$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Subspace sum, span, lattice join, lattice supremum
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Define subspace sum in ` SH ` .  See ~ shsval , ~ shsval2i , and
       ~ shsval3i for its value. $)
    df-shs $a |- +H = ( x e. SH , y e. SH |-> ( +h " ( x X. y ) ) ) $.

    $( Define the linear span of a subset of Hilbert space.  Definition of span
       in [Schechter] p. 276.  See ~ spanval for its value. $)
    df-span $a |- span = ( x e. ~P ~H |-> |^| { y e. SH | x C_ y } ) $.

    $( Define Hilbert lattice join.  See ~ chjval for its value and ~ chjcl for
       its closure law.  Note that we define it over all Hilbert space subsets
       to allow proving more general theorems.  Even for general subsets the
       join belongs to ` CH ` ; see ~ sshjcl . $)
    df-chj $a |- vH = ( x e. ~P ~H , y e. ~P ~H |->
                        ( _|_ ` ( _|_ ` ( x u. y ) ) ) ) $.
  $}

  $( Define the supremum of a set of Hilbert lattice elements.  See ~ chsupval2
     for its value.  We actually define the supremum for an arbitrary
     collection of Hilbert space subsets, not just elements of the Hilbert
     lattice ` CH ` , to allow more general theorems.  Even for general subsets
     the supremum still a Hilbert lattice element; see ~ hsupcl . $)
  df-chsup $a |- \/H = ( x e. ~P ~P ~H |-> ( _|_ ` ( _|_ ` U. x ) ) ) $.

  ${
    $d f x y z A $.  $d x y z B $.  $d x y z C $.  $d x y D $.
    $( Value of subspace sum of two Hilbert space subspaces.  Definition of
       subspace sum in [Kalmbach] p. 65.  (Revised by Mario Carneiro,
       23-Dec-2013.) $)
    shsval $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) =
                     ( +h " ( A X. B ) ) ) $=
      ( vx vy csh cva cv cxp cima cph wceq xpeq12 imaeq2d df-shs cablo wcel cvv
      wa hilablo imaexg ax-mp ovmpt2a ) CDABEEFCGZDGZHZIFABHZIZJUCAKUDBKRUEUFFU
      CAUDBLMCDNFOPUGQPSFUFOTUAUB $.
      $( [23-Dec-2013] $) $( [16-Oct-1999] $)

    $( The subspace sum is a subset of Hilbert space.  (Contributed by Mario
       Carneiro, 23-Dec-2013.) $)
    shsss $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) C_ ~H ) $=
      ( csh wcel wa cph co cva cxp cima shsval wss crn imassrn wf ax-hfvadd frn
      chil ax-mp sstri a1i eqsstrd ) ACDBCDEZABFGHABIZJZRABKUERLUCUEHMZRHUDNRRI
      ZRHOUFRLPUGRHQSTUAUB $.
      $( [23-Dec-2013] $)

    $( Membership in the subspace sum of two Hilbert subspaces.  (Revised by
       Mario Carneiro, 23-Dec-2013.) $)
    shsel $p |- ( ( A e. SH /\ B e. SH ) -> ( C e. ( A +H B ) <->
                 E. x e. A E. y e. B C = ( x +h y ) ) ) $=
      ( csh wcel wa cph co cva cxp cima cv wceq wrex shsval chil wss shss wb wf
      eleq2d wfn ax-hfvadd ffn ax-mp xpss12 syl2an ovelimab sylancr bitrd ) CFG
      ZDFGZHZECDIJZGEKCDLZMZGZEANBNKJOBDPACPZUOUPURECDQUCUOKRRLZUDZUQVASZUSUTUA
      VARKUBVBUEVARKUFUGUMCRSDRSVCUNCTDTCRDRUHUIABVACDEKUJUKUL $.
      $( [29-Jan-2014] $) $( [14-Dec-2004] $)

    $( Membership in the subspace sum of two Hilbert subspaces, using vector
       subtraction. $)
    shsel3 $p |- ( ( A e. SH /\ B e. SH ) -> ( C e. ( A +H B ) <->
                 E. x e. A E. y e. B C = ( x -h y ) ) ) $=
      ( vz csh wcel wa co cv cva wceq wrex cmv c1 csm id chil shel shsel syl2an
      cph cneg hvaddsubval an4s anassrs sylan9eqr ax-1cn negcli shmulcl adantll
      cc mp3an2 adantlr oveq2 eqeq2d rcla4ev sylan syldan ex rexlimdva hvsubval
      impbid rexbidva bitrd ) CGHZDGHZIZECDUCJHEAKZFKZLJZMZFDNZACNEVJBKZOJZMZBD
      NZACNAFCDEUAVIVNVRACVIVJCHZIZVNVRVTVMVRFDVTVKDHZIZVMVRWBVMEVJPUDZVKQJZOJZ
      MZVRVMWBEVLWEVMRVIVSWAVLWEMZVGVSVHWAWGVGVSIZVJSHZVKSHWGVHWAIVJCTZVKDTVJVK
      UEUBUFUGUHWBWDDHZWFVRVIWAWKVSVHWAWKVGVHWCUMHZWAWKPUIUJZWCVKDUKUNULUOVQWFB
      WDDVOWDMVPWEEVOWDVJOUPUQURUSUTVAVBVTVQVNBDVTVODHZIZVQVNWOVQEVJWCVOQJZLJZM
      ZVNVQWOEVPWQVQRVIVSWNVPWQMZVGVSVHWNWSWHWIVOSHWSVHWNIWJVODTVJVOVCUBUFUGUHW
      OWPDHZWRVNVIWNWTVSVHWNWTVGVHWLWNWTWMWCVODUKUNULUOVMWRFWPDVKWPMVLWQEVKWPVJ
      LUPUQURUSUTVAVBVDVEVF $.
      $( [20-Jan-2007] $)

    ${
      shscl.1 $e |- A e. SH $.
      shscl.2 $e |- B e. SH $.
      $( Membership in subspace sum. $)
      shseli $p |- ( C e. ( A +H B )
                 <-> E. x e. A E. y e. B C = ( x +h y ) ) $=
        ( csh wcel cph co cv cva wceq wrex wb shsel mp2an ) CHIDHIECDJKIEALBLMK
        NBDOACOPFGABCDEQR $.
        $( [4-May-2000] $)

      $d x y z w f g v u A $.  $d x y z w f g v u B $.
      $( Closure of subspace sum. $)
      shscli $p |- ( A +H B ) e. SH $=
        ( vx vy vv vu vf vg co wcel chil c0v wa cv cva csm wceq wrex vz cph csh
        vw wss wral cc shsss mp2an sh0 ax-mp ax-hv0cl hvaddid2i eqcomi rcla4eov
        mp3an shseli mpbir pm3.2i shaddcl mp3an1 ad2ant2r ad2ant2l oveq12 sheli
        wi anim12i hvadd4 syl2an an4s eqtr4d syl3anc ancoms exp43 rexlimivv imp
        com3l anbi12i 3imtr4i rgen2a shmulcl adantrr adantrl adantl ad2antll id
        oveq2 ax-hvdistr1 syl3an 3expb adantrrr eqtrd exp42 impcom anbi2i rgen2
        issh2 mpbir2an ) ABUBKZUCLWSMUEZNWSLZOEPZFPZQKZWSLZFWSUFEWSUFZXBXCRKZWS
        LZFWSUFEUGUFZOWTXAAUCLZBUCLZWTCDABUHUIXANXDSFBTEATZNALZNBLZNNNQKZSXLXJX
        MCAUJUKXKXNDBUJUKXONNULUMUNEFABNNNQUOUPEFABNCDUQURUSXFXIXEEFWSXBUAPZUDP
        ZQKZSZUDBTUAATZXCGPZHPZQKZSZHBTGATZOXDIPJPQKZSJBTIATZXBWSLZXCWSLZOXEXTY
        EYGXSYEYGVFUAUDABYEXPALZXQBLZOZXSYGYDYLXSYGVFVFGHABYAALZYBBLZOZYDYLXSYG
        YLXSOZYOYDOZYGYPYQOZXPYAQKZALZXQYBQKZBLZXDYSUUAQKZSYGYLYOYTXSYDYJYMYTYK
        YNXJYJYMYTCXPYAAUTVAVBVBYLYOUUBXSYDYKYNUUBYJYMXKYKYNUUBDXQYBBUTVAVCVBYR
        XDXRYCQKZUUCXSYDXDUUDSYLYOXBXRXCYCQVDVCYLYOUUCUUDSZXSYDYJYMYKYNUUEYJYMO
        XPMLZYAMLZOXQMLZYBMLZOUUEYKYNOYJUUFYMUUGXPACVEYAACVEZVGYKUUHYNUUIXQBDVE
        YBBDVEZVGXPYAXQYBVHVIVJVBVKIJABYSUUAXDQUOVLVMVNVOVQVOVPYHXTYIYEUAUDABXB
        CDUQGHABXCCDUQZVRIJABXDCDUQVSVTXHEFUGWSXBUGLZYEOXGYFSJBTIATZUUMYIOXHYEU
        UMUUNYDUUMUUNVFZGHABYMYNYDUUOVFYMYNYDUUMUUNUUMYMYNYDOZOZUUNUUMUUQOZXBYA
        RKZALZXBYBRKZBLZXGUUSUVAQKZSUUNUUMYMUUTUUPXJUUMYMUUTCXBYAAWAVAWBUUMUUPU
        VBYMUUMYNUVBYDXKUUMYNUVBDXBYBBWAVAWBWCUURXGXBYCRKZUVCUUPXGUVDSZUUMYMYDU
        VEYNXCYCXBRWGWDWEUUMYMYNUVDUVCSZYDUUMYMYNUVFUUMUUMYMUUGYNUUIUVFUUMWFUUJ
        UUKXBYAYBWHWIWJWKWLIJABUUSUVAXGQUOVLVMWMVPVOWNYIYEUUMUULWOIJABXGCDUQVSW
        PUSEFWSWQWR $.
        $( [23-Dec-2013] $) $( [15-Oct-1999] $)
    $}

    $( Closure of subspace sum. $)
    shscl $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) e. SH ) $=
      ( csh wcel cph co chil cif oveq1 eleq1d oveq2 helsh elimel shscli dedth2h
      wceq ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATPSUACATBEIJBUBPUAUCCBUB
      TEKJTUBAGCLMBGCLMNO $.
      $( [15-Dec-2004] $)

    $( Commutative law for subspace sum. $)
    shscom $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) = ( B +H A ) ) $=
      ( vx vy vz csh wcel wa cph co cv cva wceq wrex chil shel anim12i ax-hvcom
      an4s shsel syl eqeq2d 2rexbidva rexcom syl6bb wb ancoms 3bitr4d eqrdv ) A
      FGZBFGZHZCABIJZBAIJZULCKZDKZEKZLJZMZEBNDANZUOUQUPLJZMZDANEBNZUOUMGUOUNGZU
      LUTVBEBNDANVCULUSVBDEABULUPAGZUQBGZHHZURVAUOVGUPOGZUQOGZHZURVAMUJVEUKVFVJ
      UJVEHVHUKVFHVIUPAPUQBPQSUPUQRUAUBUCVBDEABUDUEDEABUOTUKUJVDVCUFEDBAUOTUGUH
      UI $.
      $( [15-Dec-2004] $)

    $( Vector sum belongs to subspace sum. $)
    shsva $p |- ( ( A e. SH /\ B e. SH ) -> ( ( C e. A /\ D e. B ) ->
                 ( C +h D ) e. ( A +H B ) ) ) $=
      ( vx vy wcel wa cva co cph csh cv wceq wrex rcla4eov mp3an3 shsel syl5ibr
      eqid ) CAGZDBGZHCDIJZABKJGALGBLGHUCEMFMIJNFBOEAOZUAUBUCUCNUDUCTEFABCDUCIP
      QEFABUCRS $.
      $( [15-Dec-2004] $)

    $( A subspace sum contains a member of one of its subspaces. $)
    shsel1 $p |- ( ( A e. SH /\ B e. SH ) ->
                  ( C e. A -> C e. ( A +H B ) ) ) $=
      ( csh wcel wa cph co c0v cva wceq chil shel ax-hvaddid syl adantlr adantl
      sh0 shsva mpan2d imp eqeltrrd ex ) ADEZBDEZFZCAEZCABGHZEUFUGFCIJHZCUHUDUG
      UICKZUEUDUGFCLEUJCAMCNOPUFUGUIUHEZUFUGIBEZUKUEULUDBRQABCISTUAUBUC $.
      $( [15-Dec-2004] $)

    $( A subspace sum contains a member of one of its subspaces. $)
    shsel2 $p |- ( ( A e. SH /\ B e. SH ) ->
                 ( C e. B -> C e. ( A +H B ) ) ) $=
      ( csh wcel wa cph co wi shsel1 ancoms shscom eleq2d sylibrd ) ADEZBDEZFZC
      BEZCBAGHZEZCABGHZEPORTIBACJKQUASCABLMN $.
      $( [15-Dec-2004] $)

    $( Vector subtraction belongs to subspace sum. $)
    shsvs $p |- ( ( A e. SH /\ B e. SH ) ->
                ( ( C e. A /\ D e. B ) -> ( C -h D ) e. ( A +H B ) ) ) $=
      ( csh wcel wa cph w3a cmv shscl a1d shsel1 adantrd shsel2 adantld shsubcl
      co 3jcad syl6 ) AEFBEFGZCAFZDBFZGZABHRZEFZCUEFZDUEFZICDJRUEFUAUDUFUGUHUAU
      FUDABKLUAUBUGUCABCMNUAUCUHUBABDOPSCDUEQT $.
      $( [15-Dec-2004] $)

    $( Subspace sum is an upper bound of its arguments. $)
    shsub1 $p |- ( ( A e. SH /\ B e. SH ) -> A C_ ( A +H B ) ) $=
      ( vx csh wcel wa cph co cv shsel1 ssrdv ) ADEBDEFCAABGHABCIJK $.
      $( [14-Dec-2004] $)

    $( Subspace sum is an upper bound of its arguments. $)
    shsub2 $p |- ( ( A e. SH /\ B e. SH ) -> A C_ ( B +H A ) ) $=
      ( csh wcel wa cph co shsub1 shscom sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
      $( [17-Dec-2004] $)

    $( The orthocomplement of the zero subspace is the unit subspace. $)
    choc0 $p |- ( _|_ ` 0H ) = ~H $=
      ( vx vy c0h cort cfv chil cv wcel csp co cc0 wceq wral wa wi abai csh c0v
      wb wal h0elsh shocel ax-mp df-ral elch0 imbi1i albii ax-hv0cl elexi oveq2
      hi02 eqeq1d ceqsalv 3bitri sylibr biantru 3bitr4i eqriv ) ACDEZFAGZFHZUTB
      GZIJZKLZBCMZNZVAVAVEOZNUTUSHZVAVAVEPCQHVHVFSUABUTCUBUCVGVAVAUTRIJZKLZVEUT
      UKVEVBCHZVDOZBTVBRLZVDOZBTVJVDBCUDVLVNBVKVMVDVBUEUFUGVDVJBRRFUHUIVMVCVIKV
      BRUTIUJULUMUNUOUPUQUR $.
      $( [15-Oct-1999] $)

    $( The orthocomplement of the unit subspace is the zero subspace.  Does not
       require Axiom of Choice. $)
    choc1 $p |- ( _|_ ` ~H ) = 0H $=
      ( vx vy chil cort cfv c0h cv wcel c0v wceq csp co cc0 wral wa helsh ax-mp
      csh wb wss shocel simprbi shocss sseli hial0 syl mpbid elch0 sylibr ssriv
      h0elsh shococss choc0 fveq2i sseqtri eqssi ) CDEZFAUQFAGZUQHZURIJZURFHUSU
      RBGKLMJBCNZUTUSURCHZVACRHZUSVBVAOSPBURCUAQUBUSVBVAUTSUQCURVCUQCTPCUCQUDBU
      RUEUFUGURUHUIUJFFDEZDEZUQFRHFVETUKFULQVDCDUMUNUOUP $.
      $( [24-Oct-1999] $)

    ${
      $d x y $.
      $( Orthogonal complement of the empty set. $)
      chocnul $p |- ( _|_ ` (/) ) = ~H $=
        ( vx vy c0 cort cfv chil cv wcel csp co cc0 wceq wral ral0 wss 0ss ocel
        wa wb ax-mp mpbiran2 eqriv ) ACDEZFAGZUCHZUDFHZUDBGIJKLZBCMZUGBNCFOUEUF
        UHRSFPBUDCQTUAUB $.
        $( [31-Oct-2000] $)
    $}

    ${
      shintcl.1 $e |- ( A C_ SH /\ A =/= (/) ) $.
      $( Closure of intersection of a non-empty subset of ` SH ` . $)
      shintcli $p |- |^| A e. SH $=
        ( vx vy vz csh wcel chil wss c0v wa cv cva co wral csm syl elint2 com12
        cc cint c0 wne simpri wex n0 intss1 simpli sseli shss sstrd sylbi ax-mp
        exlimiv ax-hv0cl elexi sh0 mprgbir pm3.2i elinti shaddcl syl3an1 3expib
        syl2and ralrimiv sylibr rgen2a shmulcl sylan2d rgen2 issh2 mpbir2an
        ovex ) AUAZFGVNHIZJVNGZKCLZDLZMNZVNGZDVNOCVNOZVQVRPNZVNGZDVNOCTOZKVOVPA
        UBUCZVOAFIZWEBUDWEELZAGZEUEVOEAUFWHVOEWHVNWGHWGAUGWHWGFGZWGHIAFWGWFWEBU
        HUIZWGUJQUKUNULUMVPJWGGZEAEJAJHUOUPRWHWIWKWJWGUQQURUSWAWDVTCDVNVQVNGZVR
        VNGZKZVSWGGZEAOVTWNWOEAWHWNWOWHWLVQWGGZWMVRWGGZWOWLWHWPVQAWGUTSWMWHWQVR
        AWGUTSZWHWPWQWOWHWIWPWQWOWJVQVRWGVAVBVCVDSVEEVSAVQVRMVMRVFVGWCCDTVNVQTG
        ZWMKZWBWGGZEAOWCWTXAEAWHWTXAWHWMWQWSXAWRWHWSWQXAWHWIWSWQXAWJVQVRWGVHVBV
        CVISVEEWBAVQVRPVMRVFVJUSCDVNVKVL $.
        $( [19-Jan-2007] $) $( [14-Oct-1999] $)
    $}

    $( The intersection of a non-empty set of subspaces is a subspace. $)
    shintcl $p |- ( ( A C_ SH /\ A =/= (/) ) -> |^| A e. SH ) $=
      ( csh wss c0 wne cint wcel cif wceq inteq eleq1d sseq1 neeq1 anbi12d ssid
      wa c0h h0elsh ne0i ax-mp pm3.2i elimhyp shintcli dedth ) ABCZADEZPZAFZBGU
      GABHZFZBGABAUIIZUHUJBAUIJKUIUGUIBCZUIDEZPBBCZBDEZPABUKUEULUFUMAUIBLAUIDMN
      BUIIUNULUOUMBUIBLBUIDMNUNUOBOQBGUORBQSTUAUBUCUD $.
      $( [20-May-2007] $) $( [2-Jun-2004] $)

    ${
      chintcl.1 $e |- ( A C_ CH /\ A =/= (/) ) $.
      $( The intersection of a non-empty set of closed subspaces is a closed
         subspace. $)
      chintcli $p |- |^| A e. CH $=
        ( vf vx vy cint cch wcel csh cn cv wf chli wbr wa wi wal wss c0 wral
        simpli chsssh sstri simpri pm3.2i shintcli sseli chlimi 3exp com3r syl5
        wne vex imp ralimdva fint elint2 3imtr4g impcom gen2 isch2 mpbir2an ) A
        FZGHVCIHJVCCKZLZVDDKZMNZOVFVCHZPZDQCQAAIRASULZAGIAGRZVJBUAZUBUCVKVJBUDZ
        UEUFVICDVGVEVHVGJEKZVDLZEATVFVNHZEATVEVHVGVOVPEAVGVNAHZVOVPPZVQVNGHZVGV
        RAGVNVLUGVSVOVGVPVSVOVGVPVFVDVNDUMZUHUIUJUKUNUOEJAVDVMUPEVFAVTUQURUSUTD
        CVCVAVB $.
        $( [23-Jan-2007] $) $( [14-Oct-1999] $)
    $}

    $( The intersection (infimum) of a non-empty subset of ` CH ` belongs to
       ` CH ` .  Part of Theorem 3.13 of [Beran] p. 108.  Also part of
       Definition 3.4-1 in [MegPav2000] p. 2345 (PDF p. 8). $)
    chintcl $p |- ( ( A C_ CH /\ A =/= (/) ) -> |^| A e. CH ) $=
      ( cch wss c0 wne cint wcel cif wceq inteq eleq1d sseq1 neeq1 anbi12d ssid
      wa c0h h0elch ne0i ax-mp pm3.2i elimhyp chintcli dedth ) ABCZADEZPZAFZBGU
      GABHZFZBGABAUIIZUHUJBAUIJKUIUGUIBCZUIDEZPBBCZBDEZPABUKUEULUFUMAUIBLAUIDMN
      BUIIUNULUOUMBUIBLBUIDMNUNUOBOQBGUORBQSTUAUBUCUD $.
      $( [26-Feb-2007] $) $( [14-Oct-1999] $)

    $( Value of the linear span of a subset of Hilbert space.  The span is the
       intersection of all subspaces constraining the subset.  Definition of
       span in [Schechter] p. 276. $)
    spanval $p |- ( A C_ ~H -> ( span ` A ) = |^| { x e. SH | A C_ x } ) $=
      ( vy chil wss cpw wcel csh crab cint cvv cspn wceq ax-hilex elpw2 biimpri
      cv cfv wrex helsh sseq2 rcla4ev mpan intexrab sylib sseq1 rabbidv df-span
      inteqd fvmptg syl2anc ) BDEZBDFZGZBAQZEZAHIZJZKGZBLRURMUNULBDNOPULUPAHSZU
      SDHGULUTTUPULADHUODBUAUBUCUPAHUDUECBCQZUOEZAHIZJURUMKLVABMZVCUQVDVBUPAHVA
      BUOUFUGUICAUHUJUK $.
      $( [23-Dec-2013] $) $( [2-Jun-2004] $)

    $( Value of supremum of set of subsets of Hilbert space.  For an alternate
       version of the value, see ~ hsupval2 . $)
    hsupval $p |- ( A C_ ~P ~H -> ( \/H ` A ) = ( _|_ ` ( _|_ ` U. A ) ) ) $=
      ( vx chil cpw wss wcel chsup cfv cuni cort wceq ax-hilex pwex elpw2 unieq
      cv fveq2d df-chsup fvex fvmpt sylbir ) ACDZEAUBDZFAGHAIZJHZJHZKAUBCLMNBAB
      PZIZJHZJHUFUCGUGAKZUIUEJUJUHUDJUGAOQQBRUEJSTUA $.
      $( [23-Dec-2013] $) $( [9-Dec-2003] $)

    $( The value of the supremum of a set of closed subspaces of Hilbert
       space.  For an alternate version of the value, see ~ chsupval2 . $)
    chsupval $p |- ( A C_ CH -> ( \/H ` A ) = ( _|_ ` ( _|_ ` U. A ) ) ) $=
      ( cch wss chil cpw chsup cfv cuni cort wceq chsspwh sstr2 mpi hsupval syl
      ) ABCZADEZCZAFGAHIGIGJPBQCRKABQLMANO $.
      $( [13-Aug-2002] $)

    $( The span of a subset of Hilbert space is a subspace. $)
    spancl $p |- ( A C_ ~H -> ( span ` A ) e. SH ) $=
      ( vx chil wss cspn cfv cv csh crab cint spanval c0 wcel ssrab2 wrex helsh
      wne sseq2 rcla4ev mpan rabn0 sylibr shintcl sylancr eqeltrd ) ACDZAEFABGZ
      DZBHIZJZHBAKUFUIHDUILQZUJHMUHBHNUFUHBHOZUKCHMUFULPUHUFBCHUGCARSTUHBHUAUBU
      IUCUDUE $.
      $( [2-Jun-2004] $)

    $( A member of a span is a vector. $)
    elspancl $p |- ( ( A C_ ~H /\ B e. ( span ` A ) ) -> B e. ~H ) $=
      ( chil wss cspn cfv csh wcel spancl shel sylan ) ACDAEFZGHBLHBCHAIBLJK $.
      $( [17-Dec-2004] $)

    $( Closure of the subspace supremum of set of subsets of Hilbert space. $)
    shsupcl $p |- ( A C_ ~P ~H -> ( span ` U. A ) e. SH ) $=
      ( chil cpw wss cuni cspn cfv csh wcel uniss unipw syl6sseq spancl syl ) A
      BCZDZAEZBDQFGHIPQOEBAOJBKLQMN $.
      $( [26-Nov-2004] $)

    $( Closure of supremum of set of subsets of Hilbert space.  Note that the
       supremum belongs to ` CH ` even if the subsets do not.  (Revised by
       Mario Carneiro, 15-May-2014.) $)
    hsupcl $p |- ( A C_ ~P ~H -> ( \/H ` A ) e. CH ) $=
      ( chil cpw wss chsup cfv cuni cort cch hsupval wcel sspwuni ocss occl syl
      sylbi eqeltrd ) ABCDZAEFAGZHFZHFZIAJRSBDZUAIKZABLUBTBDUCSMTNOPQ $.
      $( [15-May-2014] $) $( [10-Nov-1999] $)

    $( Closure of supremum of subset of ` CH ` .  Definition of supremum in
       Proposition 1 of [Kalmbach] p. 65.  Shows that ` CH ` is a complete
       lattice.  Also part of Definition 3.4-1 in [MegPav2000] p. 2345 (PDF
       p. 8). $)
    chsupcl $p |- ( A C_ CH -> ( \/H ` A ) e. CH ) $=
      ( cch wss chil cpw chsup cfv wcel chsspwh sstr2 mpi hsupcl syl ) ABCZADEZ
      CZAFGBHNBOCPIABOJKALM $.
      $( [10-Nov-1999] $)

    $( Subset relation for supremum of Hilbert space subsets.  (Revised by
       Mario Carneiro, 15-May-2014.) $)
    hsupss $p |- ( ( A C_ ~P ~H /\ B C_ ~P ~H ) ->
                  ( A C_ B -> ( \/H ` A ) C_ ( \/H ` B ) ) ) $=
      ( chil cpw wss cuni cort cfv chsup uniss sspwuni occon2 syl2anb syl5 wceq
      wa wi hsupval adantr adantl sseq12d sylibrd ) ACDZEZBUCEZPZABEZAFZGHGHZBF
      ZGHGHZEZAIHZBIHZEUGUHUJEZUFULABJUDUHCEUJCEUOULQUEACKBCKUHUJLMNUFUMUIUNUKU
      DUMUIOUEARSUEUNUKOUDBRTUAUB $.
      $( [15-May-2014] $) $( [24-Nov-2004] $)

    $( Subset relation for supremum of subset of ` CH ` . $)
    chsupss $p |- ( ( A C_ CH /\ B C_ CH ) ->
                  ( A C_ B -> ( \/H ` A ) C_ ( \/H ` B ) ) ) $=
      ( cch wss chil cpw chsup cfv wi chsspwh sstr2 mpi hsupss syl2an ) ACDZAEF
      ZDZBPDZABDAGHBGHDIBCDZOCPDZQJACPKLSTRJBCPKLABMN $.
      $( [13-Aug-2002] $)

    $( The union of a set of Hilbert space subsets is smaller than its
       supremum.  (Revised by Mario Carneiro, 15-May-2014.) $)
    hsupunss $p |- ( A C_ ~P ~H -> U. A C_ ( \/H ` A ) ) $=
      ( chil cpw wss cuni cort cfv chsup sspwuni ococss sylbi hsupval sseqtr4d
      ) ABCDZAEZOFGFGZAHGNOBDOPDABIOJKALM $.
      $( [15-May-2014] $) $( [24-Nov-2004] $)

    $( The union of a set of closed subspaces is smaller than its supremum. $)
    chsupunss $p |- ( A C_ CH -> U. A C_ ( \/H ` A ) ) $=
      ( cch wss chil cpw cuni chsup cfv chsspwh sstr mpan2 hsupunss syl ) ABCZA
      DEZCZAFAGHCNBOCPIABOJKALM $.
      $( [14-Aug-2002] $)

    $( A subset of Hilbert space is included in its span. $)
    spanss2 $p |- ( A C_ ~H -> A C_ ( span ` A ) ) $=
      ( vx chil wss cv csh crab cint cspn cfv ssintub spanval syl5sseqr ) ACDAB
      EDBFGHAAIJBAFKBALM $.
      $( [2-Jun-2004] $)

    $( The union of a set of subspaces is smaller than its supremum. $)
    shsupunss $p |- ( A C_ SH -> U. A C_ ( span ` U. A ) ) $=
      ( csh wss cuni chil cspn cfv cpw shsspwh mpan2 uniss syl syl6sseq spanss2
      sstr unipw ) ABCZADZECRRFGCQREHZDZEQASCZRTCQBSCUAIABSOJASKLEPMRNL $.
      $( [26-Nov-2004] $)

    $( A subspace of Hilbert space is its own span. $)
    spanid $p |- ( A e. SH -> ( span ` A ) = A ) $=
      ( vx csh wcel cspn cfv cv wss crab cint chil wceq shss spanval syl intmin
      eqtrd ) ACDZAEFZABGHBCIJZARAKHSTLAMBANOBACPQ $.
      $( [2-Jun-2004] $)

    $( Ordering relationship for the spans of subsets of Hilbert space. $)
    spanss $p |- ( ( B C_ ~H /\ A C_ B ) -> ( span ` A ) C_ ( span ` B ) ) $=
      ( vx chil wss wa cv csh crab cint cspn cfv wi wral sstr2 ralrimivw ss2rab
      syl wceq spanval sylibr intss adantl sstr ancoms adantr 3sstr4d ) BDEZABE
      ZFZACGZEZCHIZJZBUKEZCHIZJZAKLZBKLZUIUNUQEZUHUIUPUMEZUTUIUOULMZCHNVAUIVBCH
      ABUKOPUOULCHQUAUPUMUBRUCUJADEZURUNSUIUHVCABDUDUECATRUHUSUQSUICBTUFUG $.
      $( [2-Jun-2004] $)

    $( The span of a subset of Hilbert space is less than or equal to its
       closure (double orthogonal complement). $)
    spanssoc $p |- ( A C_ ~H -> ( span ` A ) C_ ( _|_ ` ( _|_ ` A ) ) ) $=
      ( chil wss cspn cfv cort ocss ococss spanss syl2anc wcel wceq ocsh spanid
      syl csh 3syl sseqtrd ) ABCZADEZAFEZFEZDEZUBSUBBCZAUBCTUCCSUABCZUDAGZUAGOA
      HAUBIJSUEUBPKUCUBLUFUAMUBNQR $.
      $( [3-Jun-2004] $)

    $( Value of join for subsets of Hilbert space. $)
    sshjval $p |- ( ( A C_ ~H /\ B C_ ~H ) -> ( A vH B ) =
        ( _|_ ` ( _|_ ` ( A u. B ) ) ) ) $=
      ( vx vy chil wss cpw wcel chj co cun cort cfv ax-hilex elpw2 cv wa uneq12
      wceq fveq2d df-chj fvex ovmpt2a syl2anbr ) AEFAEGZHBUEHABIJABKZLMZLMZSBEF
      AENOBENOCDABUEUECPZDPZKZLMZLMUHIUIASUJBSQZULUGLUMUKUFLUIAUJBRTTCDUAUGLUBU
      CUD $.
      $( [23-Dec-2013] $) $( [1-Nov-2000] $)

    $( Value of join in ` SH ` . $)
    shjval $p |- ( ( A e. SH /\ B e. SH ) -> ( A vH B ) =
        ( _|_ ` ( _|_ ` ( A u. B ) ) ) ) $=
      ( csh wcel chil wss chj co cun cort cfv wceq shss sshjval syl2an ) ACDAEF
      BEFABGHABIJKJKLBCDAMBMABNO $.
      $( [9-Aug-2000] $)

    $( Value of join in ` CH ` . $)
    chjval $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH B ) =
        ( _|_ ` ( _|_ ` ( A u. B ) ) ) ) $=
      ( cch wcel csh chj co cun cort cfv wceq chsh shjval syl2an ) ACDAEDBEDABF
      GABHIJIJKBCDALBLABMN $.
      $( [9-Aug-2000] $)

    ${
      chjval.1 $e |- A e. CH $.
      chjval.2 $e |- B e. CH $.
      $( Value of join in ` CH ` . $)
      chjvali $p |- ( A vH B ) = ( _|_ ` ( _|_ ` ( A u. B ) ) ) $=
        ( cch wcel chj co cun cort cfv wceq chjval mp2an ) AEFBEFABGHABIJKJKLCD
        ABMN $.
        $( [9-Aug-2000] $)
    $}

    $( Value of join for subsets of Hilbert space in terms of supremum: the
       join is the supremum of its two arguments.  Based on the definition of
       join in [Beran] p. 3.  For later convenience we prove a general version
       that works for any subset of Hilbert space, not just the elements of the
       lattice ` CH ` . $)
    sshjval3 $p |- ( ( A C_ ~H /\ B C_ ~H ) -> ( A vH B ) =
                          ( \/H ` { A , B } ) ) $=
      ( chil wss wa cpr cuni cort cfv cun chsup chj co wcel wceq ax-hilex elpw2
      cpw syl2anbr fveq2d uniprg prssi hsupval syl sshjval 3eqtr4rd ) ACDZBCDZE
      ZABFZGZHIZHIZABJZHIZHIUJKIZABLMUIULUOHUIUKUNHUGACRZNZBUQNZUKUNOUHACPQZBCP
      QZABUQUQUASTTUIUJUQDZUPUMOUGURUSVBUHUTVAABUQUBSUJUCUDABUEUF $.
      $( [23-Dec-2013] $) $( [2-Mar-2004] $)

    $( Closure of join for subsets of Hilbert space. $)
    sshjcl $p |- ( ( A C_ ~H /\ B C_ ~H ) -> ( A vH B ) e. CH ) $=
      ( chil wss wa chj co cun cort cfv cch sshjval wcel unss ocss occl eqeltrd
      syl sylbi ) ACDBCDEZABFGABHZIJZIJZKABLTUACDZUCKMZABCNUDUBCDUEUAOUBPRSQ $.
      $( [1-Nov-2000] $)

    $( Closure of join in ` SH ` . $)
    shjcl $p |- ( ( A e. SH /\ B e. SH ) -> ( A vH B ) e. CH ) $=
      ( csh wcel chil wss chj co cch shss sshjcl syl2an ) ACDAEFBEFABGHIDBCDAJB
      JABKL $.
      $( [2-Nov-1999] $)

    $( Closure of join in ` CH ` . $)
    chjcl $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH B ) e. CH ) $=
      ( cch wcel csh chj co chsh shjcl syl2an ) ACDAEDBEDABFGCDBCDAHBHABIJ $.
      $( [2-Nov-1999] $)

    $( Commutative law for Hilbert lattice join of subspaces. $)
    shjcom $p |- ( ( A e. SH /\ B e. SH ) -> ( A vH B ) = ( B vH A ) ) $=
      ( csh wcel wa chj co cun cort cfv shjval wceq ancoms fveq2i syl6eq eqtr4d
      uncom ) ACDZBCDZEZABFGABHZIJZIJZBAFGZABKTUDBAHZIJZIJZUCSRUDUGLBAKMUFUBIUE
      UAIBAQNNOP $.
      $( [22-Jun-2004] $)

    $( Subset implies subset of subspace sum.  (Contributed by Mario Carneiro,
       15-May-2014.) $)
    shless $p |- ( ( ( A e. SH /\ B e. SH /\ C e. SH ) /\ A C_ B ) ->
                   ( A +H C ) C_ ( B +H C ) ) $=
      ( vx vy vz csh wcel w3a wss wa cph co cv cva wceq wrex wb shsel syl2anc
      wi ssrexv adantl simpl1 simpl3 simpl2 3imtr4d ssrdv ) AGHZBGHZCGHZIZABJZK
      ZDACLMZBCLMZUNDNZENFNOMPFCQZEAQZUREBQZUQUOHZUQUPHZUMUSUTUAULUREABUBUCUNUI
      UKVAUSRUIUJUKUMUDUIUJUKUMUEZEFACUQSTUNUJUKVBUTRUIUJUKUMUFVCEFBCUQSTUGUH
      $.
      $( [15-May-2014] $)

    $( Add disjunct to both sides of Hilbert subspace ordering.  (Revised by
       Mario Carneiro, 15-May-2014.) $)
    shlej1 $p |- ( ( ( A e. SH /\ B e. SH /\ C e. SH ) /\
                 A C_ B ) -> ( A vH C ) C_ ( B vH C ) ) $=
      ( csh wcel w3a wss wa cun cort cfv chj chil shss syl unssd syl2anc shjval
      co wceq simpr unss1 wi simpl1 simpl3 simpl2 occon2 syl5 mpd 3sstr4d ) ADE
      ZBDEZCDEZFZABGZHZACIZJKJKZBCIZJKJKZACLSZBCLSZUPUOURUTGZUNUOUAUOUQUSGZUPVC
      ABCUBUPUQMGUSMGVDVCUCUPACMUPUKAMGUKULUMUOUDZANOUPUMCMGUKULUMUOUEZCNOZPUPB
      CMUPULBMGUKULUMUOUFZBNOVGPUQUSUGQUHUIUPUKUMVAURTVEVFACRQUPULUMVBUTTVHVFBC
      RQUJ $.
      $( [15-May-2014] $) $( [22-Jun-2004] $)

    $( Add disjunct to both sides of Hilbert subspace ordering. $)
    shlej2 $p |- ( ( ( A e. SH /\ B e. SH /\ C e. SH ) /\
                 A C_ B ) -> ( C vH A ) C_ ( C vH B ) ) $=
      ( csh wcel w3a wss wa chj co shlej1 shjcom 3adant2 adantr 3adant1 3sstr3d
      wceq ) ADEZBDEZCDEZFZABGZHACIJZBCIJZCAIJZCBIJZABCKUAUCUEQZUBRTUGSACLMNUAU
      DUFQZUBSTUHRBCLONP $.
      $( [3-Jan-2007] $) $( [22-Jun-2004] $)

    shincl.1 $e |- A e. SH $.
    shincl.2 $e |- B e. SH $.
    $( Closure of intersection of two subspaces. $)
    shincli $p |- ( A i^i B ) e. SH $=
      ( cpr cint cin csh elexi intpr wss c0 wne wcel wa prss mpbi prnz shintcli
      pm3.2i eqeltrri ) ABEZFABGHABAHCIZBHDIZJUBUBHKZUBLMAHNZBHNZOUEUFUGCDTABHU
      CUDPQABUCRTSUA $.
      $( [19-Oct-1999] $)

    $( Commutative law for subspace sum. $)
    shscomi $p |- ( A +H B ) = ( B +H A ) $=
      ( csh wcel cph co wceq shscom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [17-Oct-1999] $)

    $( Vector sum belongs to subspace sum. $)
    shsvai $p |- ( ( C e. A /\ D e. B ) -> ( C +h D ) e. ( A +H B ) ) $=
      ( csh wcel wa cva co cph wi shsva mp2an ) AGHBGHCAHDBHICDJKABLKHMEFABCDNO
      $.
      $( [17-Oct-1999] $)

    $( A subspace sum contains a member of one of its subspaces. $)
    shsel1i $p |- ( C e. A -> C e. ( A +H B ) ) $=
      ( csh wcel cph co wi shsel1 mp2an ) AFGBFGCAGCABHIGJDEABCKL $.
      $( [19-Oct-1999] $)

    $( A subspace sum contains a member of one of its subspaces. $)
    shsel2i $p |- ( C e. B -> C e. ( A +H B ) ) $=
      ( csh wcel cph co wi shsel2 mp2an ) AFGBFGCBGCABHIGJDEABCKL $.
      $( [19-Oct-1999] $)

    $( Vector subtraction belongs to subspace sum. $)
    shsvsi $p |- ( ( C e. A /\ D e. B ) -> ( C -h D ) e. ( A +H B ) ) $=
      ( csh wcel wa cmv co cph wi shsvs mp2an ) AGHBGHCAHDBHICDJKABLKHMEFABCDNO
      $.
      $( [19-Oct-1999] $)

    $( Union is smaller than subspace sum. $)
    shunssi $p |- ( A u. B ) C_ ( A +H B ) $=
      ( vx vy vz co cv wcel cva wceq wrex c0v sheli eqcomd syl csh sh0 ax-mp wo
      cun cph chil ax-hvaddid rcla4eov mp3an2 mpdan hvaddid2 mp3an1 jaoi shseli
      elun 3imtr4i ssriv ) EABUBZABUCHZEIZAJZURBJZUAURFIGIKHLGBMFAMZURUPJURUQJU
      SVAUTUSURURNKHZLZVAUSURUDJZVCURACOVDVBURURUEPQUSNBJZVCVABRJVEDBSTFGABURNU
      RKUFUGUHUTURNURKHZLZVAUTVDVGURBDOVDVFURURUIPQNAJZUTVGVAARJVHCASTFGABNURUR
      KUFUJUHUKURABUMFGABURCDULUNUO $.
      $( [18-Oct-1999] $)

    $( Union is smaller than Hilbert lattice join.  (Revised by Mario Carneiro,
       15-May-2014.) $)
    shunssji $p |- ( A u. B ) C_ ( A vH B ) $=
      ( cun cort cfv chj co chil wss shssii unssi ococss ax-mp wcel wceq shjval
      csh mp2an sseqtr4i ) ABEZUBFGFGZABHIZUBJKUBUCKABJACLBDLMUBNOASPBSPUDUCQCD
      ABRTUA $.
      $( [15-May-2014] $) $( [11-Jun-2004] $)

    $( Subspace sum is smaller than Hilbert lattice join.  Remark in [Kalmbach]
       p. 65.  (Revised by Mario Carneiro, 15-May-2014.) $)
    shsleji $p |- ( A +H B ) C_ ( A vH B ) $=
      ( vx vy vz cph co chj cv wcel cva wceq wrex shseli wa sstri sseli csh cun
      wi ssun1 shunssji ssun2 cch shjcl chshii shaddcl mp3an1 syl2an eleq1a syl
      mp2an rexlimivv sylbi ssriv ) EABHIZABJIZEKZURLUTFKZGKZMIZNZGBOFAOUTUSLZF
      GABUTCDPVDVEFGABVAALZVBBLZQVCUSLZVDVEUBVFVAUSLZVBUSLZVHVGAUSVAAABUAZUSABU
      CABCDUDZRSBUSVBBVKUSBAUEVLRSUSTLVIVJVHUSATLBTLUSUFLCDABUGUNUHVAVBUSUIUJUK
      VCUSUTULUMUOUPUQ $.
      $( [15-May-2014] $) $( [19-Oct-1999] $)

    $( Commutative law for join in ` SH ` . $)
    shjcomi $p |- ( A vH B ) = ( B vH A ) $=
      ( csh wcel chj co wceq shjcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [19-Oct-1999] $)

    $( Subspace sum is an upper bound of its arguments. $)
    shsub1i $p |- A C_ ( A +H B ) $=
      ( vx cph co cv shsel1i ssriv ) EAABFGABEHCDIJ $.
      $( [19-Oct-1999] $)

    $( Subspace sum is an upper bound of its arguments. $)
    shsub2i $p |- A C_ ( B +H A ) $=
      ( vx cph co cv shsel2i ssriv ) EABAFGBAEHDCIJ $.
      $( [17-Dec-2004] $)

    $( Hilbert lattice join is an upper bound of two subspaces. $)
    shub1i $p |- A C_ ( A vH B ) $=
      ( cph co chj shsub1i shsleji sstri ) AABEFABGFABCDHABCDIJ $.
      $( [19-Oct-1999] $)

    $( Closure of ` CH ` join. $)
    shjcli $p |- ( A vH B ) e. CH $=
      ( csh wcel chj co cch shjcl mp2an ) AEFBEFABGHIFCDABJK $.
      $( [19-Oct-1999] $)

    $( ` SH ` closure of join. $)
    shjshcli $p |- ( A vH B ) e. SH $=
      ( chj co shjcli chshii ) ABEFABCDGH $.
      $( [5-May-2000] $)

    shless.1 $e |- C e. SH $.
    $( Subset implies subset of subspace sum. $)
    shlessi $p |- ( A C_ B -> ( A +H C ) C_ ( B +H C ) ) $=
      ( csh wcel wss cph co wi w3a shless ex mp3an ) AGHZBGHZCGHZABIZACJKBCJKIZ
      LDEFQRSMTUAABCNOP $.
      $( [18-Nov-2000] $)

    $( Add disjunct to both sides of Hilbert subspace ordering.  (Revised by
       Mario Carneiro, 15-May-2014.) $)
    shlej1i $p |- ( A C_ B -> ( A vH C ) C_ ( B vH C ) ) $=
      ( csh wcel wss chj co wi w3a shlej1 ex mp3an ) AGHZBGHZCGHZABIZACJKBCJKIZ
      LDEFQRSMTUAABCNOP $.
      $( [15-May-2014] $) $( [19-Oct-1999] $)

    $( Add disjunct to both sides of Hilbert subspace ordering. $)
    shlej2i $p |- ( A C_ B -> ( C vH A ) C_ ( C vH B ) ) $=
      ( wss chj co shlej1i shjcomi 3sstr4g ) ABGACHIBCHICAHICBHIABCDEFJCAFDKCBF
      EKL $.
      $( [19-Oct-1999] $)
  $}

  $( Subspace sum is smaller than subspace join.  Remark in [Kalmbach]
     p. 65. $)
  shslej $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) C_ ( A vH B ) ) $=
    ( csh wcel cph co chj wss chil cif oveq1 sseq12d oveq2 helsh elimel shsleji
    wceq dedth2h ) ACDZBCDZABEFZABGFZHSAIJZBEFZUCBGFZHUCTBIJZEFZUCUFGFZHABIIAUC
    QUAUDUBUEAUCBEKAUCBGKLBUFQUDUGUEUHBUFUCEMBUFUCGMLUCUFAICNOBICNOPR $.
    $( [12-Jul-2004] $)

  $( Closure of intersection of two subspaces. $)
  shincl $p |- ( ( A e. SH /\ B e. SH ) -> ( A i^i B ) e. SH ) $=
    ( csh wcel cin chil wceq ineq1 eleq1d ineq2 helsh elimel shincli dedth2h
    cif ) ACDZBCDZABEZCDPAFOZBEZCDSQBFOZEZCDABFFASGRTCASBHIBUAGTUBCBUASJISUAAFC
    KLBFCKLMN $.
    $( [24-Jun-2004] $)

  $( Hilbert lattice join is an upper bound of two subspaces. $)
  shub1 $p |- ( ( A e. SH /\ B e. SH ) -> A C_ ( A vH B ) ) $=
    ( csh wcel wa cph co chj shsub1 shslej sstrd ) ACDBCDEAABFGABHGABIABJK $.
    $( [22-Jun-2004] $)

  $( A subspace is a subset of its Hilbert lattice join with another. $)
  shub2 $p |- ( ( A e. SH /\ B e. SH ) -> A C_ ( B vH A ) ) $=
    ( csh wcel wa chj co shub1 shjcom sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
    $( [22-Jun-2004] $)

  ${
    $d x y z A $.
    shsidm.1 $e |- A e. SH $.
    $( Idempotent law for Hilbert subspace sum. $)
    shsidmi $p |- ( A +H A ) = A $=
      ( vx vy vz cph co wcel cva wceq wrex shseli csh shaddcl mp3an1 syl5ibrcom
      cv wa eleq1 rexlimivv sylbi ssriv shsub1i eqssi ) AAFGZACUEACQZUEHUFDQZEQ
      ZIGZJZEAKDAKUFAHZDEAAUFBBLUJUKDEAAUGAHZUHAHZRUKUJUIAHZAMHULUMUNBUGUHANOUF
      UIASPTUAUBAABBUCUD $.
      $( [6-Jun-2004] $)
  $}

  ${
    shslub.1 $e |- A e. SH $.
    shslub.2 $e |- B e. SH $.
    shslub.3 $e |- C e. SH $.
    $( Least upper bound law for Hilbert subspace sum. $)
    shslubi $p |- ( ( A C_ C /\ B C_ C ) <-> ( A +H B ) C_ C ) $=
      ( wss wa cph co shlessi shscomi syl6sseq shsidmi sylan9ss shsub1i shsub2i
      sstr mpan jca impbii ) ACGZBCGZHABIJZCGZUBUCUDBCIJZCUBUDCBIJUFACBDFEKCBFE
      LMUCUFCCIJCBCCEFFKCFNMOUEUBUCAUDGUEUBABDEPAUDCRSBUDGUEUCBAEDQBUDCRSTUA $.
      $( [15-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    shlesb1.1 $e |- A e. SH $.
    shlesb1.2 $e |- B e. SH $.
    $( Hilbert lattice ordering in terms of subspace sum. $)
    shlesb1i $p |- ( A C_ B <-> ( A +H B ) = B ) $=
      ( wss cph wceq ssid biantrur shslubi shsub2i eqss mpbiran2 shscomi sseq1i
      wa co bitr2i 3bitri ) ABEZBBEZTPBAFQZBEZABFQZBGZUATBHIBABDCDJUEUDBEZUCUEU
      FBUDEBADCKUDBLMUDUBBABCDNORS $.
      $( [23-Nov-2004] $)

    $( An alternate way to express subspace sum. $)
    shsval2i $p |- ( A +H B ) = |^| { x e. SH | ( A u. B ) C_ x } $=
      ( cph co cun cv wss csh crab cint wa ssun1 ssintub sstri ssun2 wcel mp2an
      pm3.2i wne ssrab2 wrex shscli shunssi sseq2 rcla4ev rabn0 shintcl shslubi
      c0 mpbir mpbi elrab mpbir2an intss1 ax-mp eqssi ) BCFGZBCHZAIZJZAKLZMZBVE
      JZCVEJZNUTVEJVFVGBVAVEBCOAVAKPZQCVAVECBRVHQUABCVEDEVDKJVDULUBZVEKSVCAKUCV
      IVCAKUDZUTKSZVAUTJZVJBCDEUEZBCDEUFZVCVLAUTKVBUTVAUGZUHTVCAKUIUMVDUJTUKUNU
      TVDSZVEUTJVPVKVLVMVNVCVLAUTKVOUOUPUTVDUQURUS $.
      $( [25-Nov-2004] $)

    $( An alternate way to express subspace sum. $)
    shsval3i $p |- ( A +H B ) = ( span ` ( A u. B ) ) $=
      ( vx cph co cun cv wss csh crab cint cspn shsval2i chil wceq shssii unssi
      cfv spanval ax-mp eqtr4i ) ABFGABHZEIJEKLMZUDNTZEABCDOUDPJUFUEQABPACRBDRS
      EUDUAUBUC $.
      $( [25-Nov-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    shmod.1 $e |- A e. SH $.
    shmod.2 $e |- B e. SH $.
    shmod.3 $e |- C e. SH $.
    $( The modular law holds for subspace sum.  Similar to part of Theorem 16.9
       of [MaedaMaeda] p. 70. $)
    shmodsi $p |- ( A C_ C -> ( ( A +H B ) i^i C ) C_ ( A +H ( B i^i C ) ) ) $=
      ( vz vx vy cph co cin cv wcel wa elin wceq wi chil sheli wss cva wrex cmv
      shseli wb w3a hvsubadd syl3an eqcom syl6bb 3expb shsvsi syl6eleq shlesb1i
      shscomi biimpi eleq2d syl5ib eleq1 biimpd sylan9 anim2d syl6ibr ex ancoms
      com13 anasss sylbird imp shincli shsvai syl5ibr exp3a com12 ad2antrl syld
      exp31 rexlimdvv syl5bi imp3a ssrdv ) ACUAZGABJKZCLZABCLZJKZGMZWENWHWDNZWH
      CNZOWCWHWGNZWHWDCPWCWIWJWKWJWIWCWKWIWHHMZIMZUBKZQZIBUCHAUCWJWCWKRZHIABWHD
      EUEWJWOWPHIABWJWLANZWMBNZOZWOWPWJWSOZWOOWCWMWFNZWKWTWOWCXARZWTWOWHWLUDKZW
      MQZXBWJWQWRXDWOUFWJWQWRUGXDWNWHQZWOWJWHSNWQWLSNWRWMSNXDXEUFWHCFTWLADTWMBE
      TWHWLWMUHUIWNWHUJUKULWJWQWRXDXBRZWRWJWQOZXFWCXDWRXGOZXAWCXDXHXARWCXDOZXHW
      RWMCNZOXAXIXGXJWRWCXGXCCNZXDXJXGXCACJKZNWCXKXGXCCAJKXLCAWHWLFDUMCAFDUPUNW
      CXLCXCWCXLCQACDFUOUQURUSXDXKXJXCWMCUTVAVBVCWMBCPVDVEVGVFVHVIVJWTWOXAWKRZW
      QWOXMRWJWRWOWQXMWOWQXAWKWQXAOWKWOWNWGNAWFWLWMDBCEFVKVLWHWNWGUTVMVNVOVPVJV
      QVRVSVTVGWAVTWB $.
      $( [23-Nov-2004] $)

    $( The modular law is implied by the closure of subspace sum.  Part of
       proof of Theorem 16.9 of [MaedaMaeda] p. 70. $)
    shmodi $p |- ( ( ( A +H B ) = ( A vH B ) /\ A C_ C ) ->
                ( ( A vH B ) i^i C ) C_ ( A vH ( B i^i C ) ) ) $=
      ( cph co chj wceq wss cin shmodsi ineq1 sseq1d syl5ib imp shincli shsleji
      wa syl6ss ) ABGHZABIHZJZACKZTUCCLZABCLZGHZAUGIHUDUEUFUHKZUEUBCLZUHKUDUIAB
      CDEFMUDUJUFUHUBUCCNOPQAUGDBCEFRSUA $.
      $( [23-Nov-2004] $)
  $}

