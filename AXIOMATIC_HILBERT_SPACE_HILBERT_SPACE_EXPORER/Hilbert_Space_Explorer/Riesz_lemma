$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Riesz lemma
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

    $( A continuous linear functional can be expressed as an inner product.
       Existence part of Theorem 3.9 of [Beran] p. 104. $)
    riesz3i $p |- E. w e. ~H A. v e. ~H ( T ` v ) = ( v .ih w ) $=
      ( cfv csp co wceq chil c0h c0v wcel wa cc0 cc adantr syl3anc syl2anc cmin
      vu cv wral wrex cort ax-hv0cl wf lnfnfi fveq2 nlelchi ococi choc0 3eqtr3g
      eleq2d biimpar elnlfn2 sylancr hi02 adantl eqtr4d ralrimiva oveq2 ralbidv
      cnl eqeq2d rcla4ev wne choccli chne0i wi cheli cdiv ccj csm ffvelrni hicl
      anidms his6 necon3bid divcl cjcl syl simpl hvmulcl adantll cmul cmv sylan
      ancoms his2sub simpr ax-his3 oveq12d eqtr2d hvsubcl lnfnsubi mulcom eqtrd
      lnfnmuli sylan2 mulcl syl2an subid 3eqtrd wb elnlfn ax-mp sylanbrc chssii
      wss ocorth anassrs syl2anr subeq0 mpbid adantlr jca adantlll mpbird div23
      divmul3 simpll his52 eqtr3d ex mpdan rexlimiv sylbi pm2.61ine ) BUBZCFZYJ
      AUBZGHZIZBJUCZAJUDZCVDFZUEFZKYRKIZLJMYKYJLGHZIZBJUCZYPUFYSUUABJYSYJJMZNZY
      KOYTUUDJPCUGZYJYQMZYKOICDUHZYSUUFUUCYSYQJYJYSYRUEFKUEFYQJYRKUEUIYQCDEUJZU
      KULUMUNUOYJCUPUQUUCYTOIYSYJURUSUTVAYOUUBALJYLLIZYNUUABJUUIYMYTYKYLLYJGVBV
      EVCVFUQYRKVGUAUBZLVGZUAYRUDYPUAYRYQUUHVHZVIUUKYPUAYRUUJYRMZUUJJMZUUKYPVJU
      UJYRUULVKUUMUUNNZUUKYPUUOUUKNZUUJCFZUUJUUJGHZVLHZVMFZUUJVNHZJMZYKYJUVAGHZ
      IZBJUCZYPUUNUUKUVBUUMUUNUUKNZUUTPMZUUNUVBUVFUUSPMZUVGUVFUUQPMZUURPMZUUROV
      GZUVHUUNUVIUUKJPUUJCUUGVOZQZUUNUVJUUKUUNUVJUUJUUJVPVQZQZUUNUVKUUKUUNUUROU
      UJLUUJVRVSUOZUUQUURVTRZUUSWAWBUUNUUKWCUUTUUJWDSWEUUPUVDBJUUPUUCNZUUQYJUUJ
      GHZWFHZUURVLHZYKUVCUVRUWAYKIZUVTYKUURWFHZIZUUOUUCUWDUUKUUOUUCNZUVTUWCTHZO
      IZUWDUWEUWFUUQYJVNHZYKUUJVNHZWGHZUUJGHZOUUNUUCUWFUWKIUUMUUNUUCNZUWKUWHUUJ
      GHZUWIUUJGHZTHZUWFUWLUWHJMZUWIJMZUUNUWKUWOIUUNUVIUUCUWPUVLUUQYJWDWHZUUCUU
      NUWQUUCYKPMZUUNUWQJPYJCUUGVOZYKUUJWDWHWIZUUNUUCWCZUWHUWIUUJWJRUWLUWMUVTUW
      NUWCTUWLUVIUUCUUNUWMUVTIUUNUVIUUCUVLQZUUNUUCWKUXBUUQYJUUJWLRUWLUWSUUNUUNU
      WNUWCIUUCUWSUUNUWTUSUXBUXBYKUUJUUJWLRWMWNWEUUMUUNUUCUWKOIZUWLUUMUXDUWLUWJ
      YQMZUUMUXDUWLUWJJMZUWJCFZOIZUXEUWLUWPUWQUXFUWRUXAUWHUWIWOSUWLUXGUWHCFZUWI
      CFZTHZUUQYKWFHZUXLTHZOUWLUWPUWQUXGUXKIUWRUXAUWHUWICDWPSUWLUXIUXLUXJUXLTUU
      NUVIUUCUXIUXLIUVLUUQYJCDWSWHUUCUUNUXJUXLIZUUCUWSUUNUXNUWTUWSUUNNUXJYKUUQW
      FHZUXLYKUUJCDWSUUNUWSUVIUXOUXLIUVLYKUUQWQWTWRWHWIWMUWLUXLPMZUXMOIUUNUVIUW
      SUXPUUCUVLUWTUUQYKXAXBUXLXCWBXDUUEUXEUXFUXHNXEUUGUWJCXFXGXHYQJXJUXEUUMNUX
      DVJYQUUHXIUWJUUJYQXKXGWHWIXLWRUUNUUCUWGUWDXEZUUMUWLUVTPMZUWCPMZUXQUWLUVIU
      VSPMZUXRUXCUUCUUNUXTYJUUJVPWIZUUQUVSXASZUUCUWSUVJUXSUUNUWTUVNYKUURXAXMUVT
      UWCXNSWEXOXPUUNUUKUUCUWBUWDXEZUUMUVFUUCNZUXRUWSUVJUVKNZUYCUUNUUCUXRUUKUYB
      XPUUCUWSUVFUWTUSUVFUYEUUCUVFUVJUVKUVOUVPXQQZUVTYKUURYARXRXSUUNUUKUUCUWAUV
      CIUUMUYDUWAUUSUVSWFHZUVCUYDUVIUXTUYEUWAUYGIUVFUVIUUCUVMQUUNUUCUXTUUKUYAXP
      UYFUUQUVSUURXTRUYDUVHUUCUUNUVCUYGIUVFUVHUUCUVQQUVFUUCWKUUNUUKUUCYBUUSYJUU
      JYCRUTXRYDVAYOUVEAUVAJYLUVAIZYNUVDBJUYHYMUVCYKYLUVAYJGVBVEVCVFSYEYFYGYHYI
      $.
      $( [20-Nov-2013] $) $( [13-Feb-2006] $)

    $( A continuous linear functional can be expressed as an inner product.
       Uniqueness part of Theorem 3.9 of [Beran] p. 104. $)
    riesz4i $p |- E! w e. ~H A. v e. ~H ( T ` v ) = ( v .ih w ) $=
      ( vu cv cfv csp co wceq chil wral wa wi cmin cc0 wcel cc syl wreu riesz3i
      r19.26 oveq12 adantl lnfnfi ffvelrni subid adantr eqtr3d ralimiaa hvsubcl
      wrex sylbir cmv oveq1 oveq12d eqeq1d rcla4v cno c2 c0v normcl recnd sqeq0
      cexp wb norm-i bitrd normsq simpl simpr his2sub2 syl3anc hvsubeq0 3bitr3d
      eqtrd sylibd syl5 rgen2a oveq2 eqeq2d ralbidv reu4 mpbir2an ) BGZCHZWFAGZ
      IJZKZBLMZALUAWKALUMWKWGWFFGZIJZKZBLMZNZWHWLKZOZFLMALMABCDEUBWRAFLWPWIWMPJ
      ZQKZBLMZWHLRZWLLRZNZWQWPWJWNNZBLMXAWJWNBLUCXEWTBLWFLRZXENWGWGPJZWSQXEXGWS
      KXFWGWIWGWMPUDUEXFXGQKZXEXFWGSRXHLSWFCCDUFUGWGUHTUIUJUKUNXDXAWHWLUOJZWHIJ
      ZXIWLIJZPJZQKZWQXDXILRZXAXMOWHWLULZWTXMBXILWFXIKZWSXLQXPWIXJWMXKPWFXIWHIU
      PWFXIWLIUPUQURUSTXDXIUTHZVAVFJZQKZXIVBKZXMWQXDXNXSXTVGXOXNXSXQQKZXTXNXQSR
      XSYAVGXNXQXIVCVDXQVETXIVHVITXDXRXLQXDXRXIXIIJZXLXDXNXRYBKXOXIVJTXDXNXBXCY
      BXLKXOXBXCVKXBXCVLXIWHWLVMVNVQURWHWLVOVPVRVSVTWKWOAFLWQWJWNBLWQWIWMWGWHWL
      WFIWAWBWCWDWE $.
      $( [13-Feb-2006] $)
  $}

  ${
    $d w v T $.
    $( A continuous linear functional can be expressed as an inner product.
       Uniqueness part of Theorem 3.9 of [Beran] p. 104.  See ~ riesz2 for the
       bounded linear functional version. $)
    riesz4 $p |- ( T e. ( LinFn i^i ConFn ) ->
                   E! w e. ~H A. v e. ~H ( T ` v ) = ( v .ih w ) ) $=
      ( clf ccnf cin wcel cv cfv csp wceq chil wral wreu cc0 csn cxp cif sselii
      co fveq1 eqeq1d ralbidv reubidv inss1 0lnfn 0cnfn mpbir2an elimel riesz4i
      elin inss2 dedth ) CDEFZGZBHZCIZUPAHJTZKZBLMZALNUPUOCLOPQZRZIZURKZBLMZALN
      CVACVBKZUTVEALVFUSVDBLVFUQVCURUPCVBUAUBUCUDABVBUNDVBDEUECVAUNVAUNGVADGVAE
      GUFUGVADEUKUHUIZSUNEVBDEULVGSUJUM $.
      $( [16-Feb-2006] $)
  $}

  ${
    $d x y z T $.
    $( Part 1 of the Riesz representation theorem for bounded linear
       functionals.  A linear functional is bounded iff its value can be
       expressed as an inner product.  Part of Theorem 17.3 of [Halmos] p. 31.
       For part 2, see ~ riesz2 .  For the continuous linear functional
       version, see ~ riesz3i and ~ riesz4 . $)
    riesz1 $p |- ( T e. LinFn -> ( ( normfn ` T ) e. RR <->
                  E. y e. ~H A. x e. ~H ( T ` x ) = ( x .ih y ) ) ) $=
      ( vz clf wcel ccnf cfv cr cv co wceq chil wral wrex wa cabs cmul cle wbr
      cnmf csp lnfncnbd cin cc0 csn cxp cif fveq1 eqeq1d rexralbidv inss1 0lnfn
      elin 0cnfn mpbir2an elimel sselii inss2 riesz3i dedth sylbir ex cno fveq2
      wi adantl bcs normcl cc recn mulcom syl2an breqtrd adantll adantr eqbrtrd
      an32s ralimdva jctild oveq1 breq2d ralbidv rcla4ev syl6 rexlimdva lnfncon
      sylibrd impbid bitr3d ) CEFZCGFZCUAHIFAJZCHZWMBJZUBKZLZAMNZBMOZCUCWKWLWSW
      KWLWSWKWLPCEGUDZFZWSCEGUNXAWSWMXACMUEUFUGZUHZHZWPLZAMNBMOCXBCXCLZWQXEBAMM
      XFWNXDWPWMCXCUIUJUKBAXCWTEXCEGULCXBWTXBWTFXBEFXBGFUMUOXBEGUNUPUQZURWTGXCE
      GUSXGURUTVAVBVCWKWSWNQHZDJZWMVDHZRKZSTZAMNZDIOZWLWKWRXNBMWKWOMFZPZWRWOVDH
      ZIFZXHXQXJRKZSTZAMNZPXNXPWRYAXRXPWQXTAMWKWMMFZXOWQXTVFWKYBPXOPZWQXTYCWQPX
      HWPQHZXSSWQXHYDLYCWNWPQVEVGYCYDXSSTZWQYBXOYEWKYBXOPYDXJXQRKZXSSWMWOVHYBXJ
      IFZXRYFXSLZXOWMVIWOVIZYGXJVJFXQVJFYHXRXJVKXQVKXJXQVLVMVMVNVOVPVQVCVRVSXOX
      RWKYIVGVTXMYADXQIXIXQLZXLXTAMYJXKXSXHSXIXQXJRWAWBWCWDWEWFDACWGWHWIWJ $.
      $( [25-Apr-2006] $)

    $( Part 2 of the Riesz representation theorem for bounded linear
       functionals.  The value of a bounded linear functional corresponds to a
       unique inner product.  Part of Theorem 17.3 of [Halmos] p. 31.  For part
       1, see ~ riesz1 . $)
    riesz2 $p |- ( ( T e. LinFn /\ ( normfn ` T ) e. RR ) ->
                   E! y e. ~H A. x e. ~H ( T ` x ) = ( x .ih y ) ) $=
      ( clf wcel cnmf cfv cr wa ccnf cin cv csp co wceq chil wral wreu lnfncnbd
      elin pm5.32i bitri riesz4 sylbir ) CDEZCFGHEZIZCDJKEZALZCGUIBLMNOAPQBPRUH
      UECJEZIUGCDJTUEUJUFCSUAUBBACUCUD $.
      $( [25-Apr-2006] $)
  $}

