$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Closed subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of closed subspaces of a Hilbert space.  A closed subspace
     is one in which the limit of every convergent sequence in the subspace
     belongs to the subspace.  For its membership relation, see ~ isch .  From
     Definition of [Beran] p. 107.  Alternate definitions are given by ~ isch2
     and ~ isch3 . $)
  df-ch $a |- CH = { h e. SH | ( ~~>v " ( h ^m NN ) ) C_ h } $.

  ${
    $d h H $.
    $( Closed subspace ` H ` of a Hilbert space.  (Contributed by Mario
       Carneiro, 23-Dec-2013.) $)
    isch $p |- ( H e. CH <-> ( H e. SH /\ ( ~~>v " ( H ^m NN ) ) C_ H ) ) $=
      ( vh chli cv cn cmap co cima wss csh cch wceq oveq1 imaeq2d sseq12d df-ch
      id elrab2 ) CBDZEFGZHZSICAEFGZHZAIBAJKSALZUAUCSAUDTUBCSAEFMNUDQOBPR $.
      $( [23-Dec-2013] $) $( [17-Aug-1999] $)
  $}

  ${
    $d x f H $.
    $( Closed subspace ` H ` of a Hilbert space.  Definition of [Beran]
       p. 107. $)
    isch2 $p |- ( H e. CH <-> ( H e. SH /\ A. f A. x ( ( f : NN --> H /\
        f ~~>v x ) -> x e. H ) ) ) $=
      ( cch wcel csh chli cn cmap co cima wss wa cv wf wbr wi wal isch cvv nnex
      wex 19.23v elima2 imbi1i bitr4i albii alcom dfss2 3bitr4i wb elmapg mpan2
      vex anbi1d imbi1d 2albidv syl5bbr pm5.32i bitri ) CDECFEZGCHIJZKZCLZMVAHC
      BNZOZVEANZGPZMZVGCEZQZARBRZMCSVAVDVLVDVEVBEZVHMZVJQZARBRZVAVLVOBRZARVGVCE
      ZVJQZARVPVDVQVSAVQVNBUBZVJQVSVNVJBUCVRVTVJBVGGVBAUNUDUEUFUGVOBAUHAVCCUIUJ
      VAVOVKBAVAVNVIVJVAVMVFVHVAHTEVMVFUKUACHVEFTULUMUOUPUQURUSUT $.
      $( [23-Dec-2013] $) $( [17-Aug-1999] $)
  $}

  $( A closed subspace is a subspace. $)
  chsh $p |- ( H e. CH -> H e. SH ) $=
    ( cch wcel csh chli cn cmap co cima wss isch simplbi ) ABCADCEAFGHIAJAKL $.
    $( [23-Dec-2013] $) $( [19-Oct-1999] $)

  $( Closed subspaces are subspaces in a Hilbert space. $)
  chsssh $p |- CH C_ SH $=
    ( vx cch csh cv chsh ssriv ) ABCADEF $.
    $( [23-Dec-2013] $) $( [29-May-1999] $)

  $( The set of closed subspaces of a Hilbert space exists (is a set). $)
  chex $p |- CH e. _V $=
    ( cch csh shex chsssh ssexi ) ABCDE $.
    $( [23-Oct-1999] $)

  ${
    chshi.1 $e |- H e. CH $.
    $( A closed subspace is a subspace. $)
    chshii $p |- H e. SH $=
      ( cch wcel csh chsh ax-mp ) ACDAEDBAFG $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d x y H $.
    $( The zero vector belongs to any closed subspace of a Hilbert space. $)
    ch0 $p |- ( H e. CH -> 0h e. H ) $=
      ( cch wcel csh c0v chsh sh0 syl ) ABCADCEACAFAGH $.
      $( [24-Aug-1999] $)

    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chss $p |- ( H e. CH -> H C_ ~H ) $=
      ( cch wcel csh chil wss chsh shss syl ) ABCADCAEFAGAHI $.
      $( [24-Aug-1999] $)

  $}

  $( A member of a closed subspace of a Hilbert space is a vector. $)
  chel $p |- ( ( H e. CH /\ A e. H ) -> A e. ~H ) $=
    ( cch wcel chil chss sselda ) BCDBEABFG $.
    $( [15-Dec-2004] $)

  ${
    chssi.1 $e |- H e. CH $.
    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chssii $p |- H C_ ~H $=
      ( chshii shssii ) AABCD $.
      $( [6-Oct-1999] $)

    $( A member of a closed subspace of a Hilbert space is a vector. $)
    cheli $p |- ( A e. H -> A e. ~H ) $=
      ( chil chssii sseli ) BDABCEF $.
      $( [6-Oct-1999] $)

    ${
      cheli.1 $e |- A e. H $.
      $( A member of a closed subspace of a Hilbert space is a vector. $)
      chelii $p |- A e. ~H $=
        ( chil chssii sselii ) BEABCFDG $.
        $( [6-Oct-1999] $)
    $}
  $}

  ${
    $d x f F $.  $d x f H $.  $d x A $.
    chlim.1 $e |- A e. _V $.
    $( The limit property of a closed subspace of a Hilbert space. $)
    chlimi $p |- ( ( H e. CH /\ F : NN --> H /\ F ~~>v A ) -> A e. H ) $=
      ( vf vx cch wcel cn wf chli wbr cv wa wi wal csh cvv wceq syl simprbi fex
      isch2 nnex mpan2 adantr breq1 anbi12d imbi1d albidv cla4gv anbi2d imbi12d
      feq1 breq2 eleq1 cla4v syl6 pm2.43b 3impib ) CGHZICBJZBAKLZACHZVAICEMZJZV
      EFMZKLZNZVGCHZOZFPZEPZVBVCNZVDOZVACQHVMFECUCUAVMVNVDVNBRHZVMVOOVBVPVCVBIR
      HVPUDICRBUBUEUFVPVMVBBVGKLZNZVJOZFPZVOVLVTEBRVEBSZVKVSFWAVIVRVJWAVFVBVHVQ
      ICVEBUNVEBVGKUGUHUIUJUKVSVOFADVGASZVRVNVJVDWBVQVCVBVGABKUOULVGACUPUMUQURT
      USTUT $.
      $( [14-Apr-2008] $) $( [14-Sep-1999] $)
  $}

  ${
    $d x y z $.
    $( The zero sequence in Hilbert space converges to the zero vector.  (The
       proof was shortened by Mario Carneiro, 14-May-2014.) $)
    hlim0 $p |- ( NN X. { 0h } ) ~~>v 0h $=
      ( cn c0v csn cxp chli wbr cva csm cop cno cims cfv copn chil wcel c1 eqid
      ax-mp ax-hv0cl wf clm cmap co ctop cz cme hhmet opntop 1z hhmetba uniopn2
      cuni wceq nnuz lmconst mp3an elexi fconst snssi mp2an ax-hilex nnex elmap
      wss fss mpbir cres wa hhlm breqi brres bitri mpbir2an ) ABCZDZBEFZVOBGHIJ
      IZKLZMLZUALZFZVONAUBUCZOZVSUDOZBNOZPUEOWAVRUFOZWDVRVQVQQZVRQZUGZVRVSVSQZU
      HRSUIBVSPNAWFNVSULUMWIVRVSNVRVQWGWHUJWJUKRUNUOUPWCANVOTZAVNVOTVNNVDZWKABB
      NSUQZURWEWLSBNUSRAVNNVOVEUTNAVOVAVBVCVFVPVOBVTWBVGZFWAWCVHVOBEWNVRVQVSWGW
      HWJVIVJVOBVTWBWMVKVLVM $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x F $.
    $( If a sequence in Hilbert space subset converges to a limit, it is a
       Cauchy sequence.  (The proof was shortened by Mario Carneiro,
       14-May-2014.) $)
    hlimcaui $p |- ( F ~~>v A -> F e. Cauchy ) $=
      ( chli wbr cdm ccau cva csm cop cno cims cfv cca cin eqid ax-mp wcel wrel
      wss eqsstri chil cn cmap co copn clm cres hhlm resss dmss cme hhmet lmcau
      sstri dmeqi dmres eqtri inss1 ssini sseqtr4i relres releqi releldm sseldi
      hhcau mpbir mpan ) BACDZCEZFBVIGHIJIZKLZMLZUAUBUCUDZNFVIVLVMVIVKUELZUFLZE
      ZVLCVOSVIVPSCVOVMUGZVOVKVJVNVJOZVKOZVNOZUHZVOVMUITCVOUJPVKUKQVPVLSVKVJVRV
      SULVKVNVTUMPUNVIVMVPNZVMVIVQEWBCVQWAUOVOVMUPUQVMVPURTUSVKVJVRVSVEUTCRZVHB
      VIQWCVQRVOVMVACVQWAVBVFBACVCVGVD $.
      $( [14-May-2014] $) $( [17-Aug-1999] $)
  $}

  $( Function-like behavior of the convergence relation.  (Contributed by Mario
     Carneiro, 14-May-2014.) $)
  hlimf $p |- ~~>v : dom ~~>v --> ~H $=
    ( vx chli cdm chil wf wfn cfv wcel wral wfun cva csm cop cno cims copn eqid
    cv clm ax-mp cn cmap co cres cme cha hhmet methaus lmfun mp2b funres funeqi
    hhlm mpbir funfn mpbi wbr funfvbrb fvex hlimveci sylbi rgen ffnfv mpbir2an
    wb ) BCZDBEBVFFZARZBGZDHZAVFIBJZVGVKKLMNMZOGZPGZSGZDUAUBUCZUDZJZVOJZVRVMUEH
    VNUFHVSVMVLVLQZVMQZUGVMVNVNQZUHVNUIUJVPVOUKTBVQVMVLVNVTWAWBUMULUNZBUOUPVJAV
    FVHVFHZVHVIBUQZVJVKWDWEVEWCVHBURTVIVHVHBUSUTVAVBAVFDBVCVD $.
    $( [14-May-2014] $)

  ${
    $d x y z w v A $.  $d x y z w v B $.  $d x y z w v F $.
    hlimuni.1 $e |- A e. _V $.
    hlimuni.2 $e |- B e. _V $.
    $( A Hilbert space sequence converges to at most one limit.  (The proof was
       shortened by Mario Carneiro, 14-May-2014.) $)
    hlimunii $p |- ( ( F ~~>v A /\ F ~~>v B ) -> A = B ) $=
      ( chli wbr cfv cdm chil wf wfun wceq wi hlimf ffun funbrfv mp2b sylan9req
      ) CAFGZCBFGZACFHZBFIZJFKZFLZTUBAMNOUCJFPZCAFDQRUDUEUAUBBMNOUFCBFEQRS $.
      $( [14-May-2014] $) $( [19-Aug-1999] $)
  $}

  ${
    $d x y F $.  $d x y H $.
    $( The limit of a Hilbert space sequence is unique. $)
    hlimreui $p |- ( E. x e. H F ~~>v x <-> E! x e. H F ~~>v x ) $=
      ( vy cv chli wbr wrex wa wceq wral wreu vex hlimunii rgen2w biantru breq2
      wi reu4 bitr4i ) BAEZFGZACHZUCUBBDEZFGZIUAUDJRZDCKACKZIUBACLUGUCUFADCCUAU
      DBAMDMNOPUBUEADCUAUDBFQST $.
      $( [14-May-2014] $) $( [19-Aug-1999] $)

    $( The limit of a Hilbert space sequence is unique.  (The proof was
       shortened by Mario Carneiro, 14-May-2014.) $)
    hlimeui $p |- ( E. x F ~~>v x <-> E! x F ~~>v x ) $=
      ( cv chli wbr cvv wrex wreu wex weu hlimreui rexv reuv 3bitr3i ) BACDEZAF
      GOAFHOAIOAJABFKOALOAMN $.
      $( [14-May-2014] $) $( [19-Aug-1999] $)
  $}

  ${
    $d x f H $.
    $( A Hilbert subspace is closed iff it is complete.  A complete subspace is
       one in which every Cauchy sequence of vectors in the subspace converges
       to a member of the subspace (Definition of complete subspace in [Beran]
       p. 96).  Remark 3.12 of [Beran] p. 107.  (The proof was shortened by
       Mario Carneiro, 23-Dec-2013.) $)
    isch3 $p |- ( H e. CH <-> ( H e. SH /\ A. f e. Cauchy
                  ( f : NN --> H -> E. x e. H f ~~>v x ) ) ) $=
      ( cch wcel csh cv wa wi wal wrex ccau wex chil rexex syl ax-17 hbim bitri
      ex cn wf chli wbr isch2 ax-hcompl 19.29 sylan2 id imp an12s simprr eximdv
      wral jca com12 df-rex syl6ibr hbre1 hlimcaui imim1i hlimeui sylib exancom
      weu biimpi eupick syl2anc syli imim2i bi2.04 impexp 3imtr4i alrimi impbii
      albii df-ral bitr4i anbi2i ) CDECFEZUACBGZUBZWAAGZUCUDZHZWCCEZIZAJZBJZHVT
      WBWDACKZIZBLUNZHABCUEWIWLVTWIWALEZWKIZBJWLWHWNBWHWNWHWMWKWHWMHWGWDHZAMZWK
      WMWHWDAMZWPWMWDANKWQAWAUFWDANOPWGWDAUGUHWPWBWFWDHZAMZWJWBWPWSWBWOWRAWBWOW
      RWBWOHWFWDWGWBWDWFWGWEWFWGUIUJUKWBWGWDULUOTUMUPWDACUQZURPTWNWGAWMWKAWMAQW
      BWJAWBAQWDACUSRRWBWMWJIZIWBWDWFIZIWNWGXAXBWBWDXAWJWFWDWMWJWCWAUTVAWJWDAVE
      ZWDWFHAMZXBWJWQXCWDACOAWAVBVCWJXDWJWSXDWTWFWDAVDSVFWDWFAVGVHVIVJWMWBWJVKW
      BWDWFVLVMVNVOVPWKBLVQVRVSS $.
      $( [14-May-2014] $) $( [24-Dec-2001] $)
  $}

  ${
    $d x f H $.  $d x f F $.
    $( Completeness of a closed subspace of Hilbert space. $)
    chcompl $p |- ( ( H e. CH /\ F e. Cauchy /\ F : NN --> H ) ->
                      E. x e. H F ~~>v x ) $=
      ( vf cch wcel ccau cn wf cv chli wbr wrex wi wral isch3 simprbi wceq feq1
      csh breq1 rexbidv imbi12d rcla4cv syl 3imp ) CEFZBGFZHCBIZBAJZKLZACMZUGHC
      DJZIZUMUJKLZACMZNZDGOZUHUIULNZNUGCTFURADCPQUQUSDBGUMBRZUNUIUPULHCUMBSUTUO
      UKACUMBUJKUAUBUCUDUEUF $.
      $( [13-Apr-2008] $) $( [4-Oct-1999] $)
  $}

  ${
    $d x y f $.
    $( The unit Hilbert lattice element (which is all of Hilbert space) belongs
       to the Hilbert lattice.  Part of Proposition 1 of [Kalmbach] p. 65. $)
    helch $p |- ~H e. CH $=
      ( vf vx vy chil cch wcel csh cn cv wf chli wbr wa wi wal co wral mpbir2an
      cc pm3.2i wss c0v cva csm ax-hv0cl hvaddcl rgen2a hvmulcl rgen2 issh2 vex
      ssid hlimveci adantl gen2 isch2 ) DEFDGFZHDAIZJZURBIZKLZMUTDFZNZBOAOUQDDU
      AZUBDFZMUTCIZUCPDFZCDQBDQZUTVFUDPDFZCDQBSQZMVDVEDULUETVHVJVGBCDUTVFUFUGVI
      BCSDUTVFUHUITBCDUJRVCABVAVBUSUTURBUKUMUNUOBADUPR $.
      $( [6-Sep-1999] $)
  $}

  $( Hilbert space is a subspace of Hilbert space. $)
  helsh $p |- ~H e. SH $=
    ( chil helch chshii ) ABC $.
    $( [2-Jun-2004] $)

  $( Subspaces are subsets of Hilbert space. $)
  shsspwh $p |- SH C_ ~P ~H $=
    ( vx csh cuni cpw chil pwuni wcel cv wss wral wceq helsh shss ssunieq mp2an
    rgen pweqi sseqtr4i ) BBCZDEDBFESEBGAHZEIZABJESKLUAABTMPAEBNOQR $.
    $( [24-Nov-2004] $)

  $( Closed subspaces are subsets of Hilbert space. $)
  chsspwh $p |- CH C_ ~P ~H $=
    ( cch csh chil cpw chsssh shsspwh sstri ) ABCDEFG $.
    $( [24-Nov-2004] $)

  ${
    $d x y f $.
    $( The zero subspace belongs to the set of closed subspaces of Hilbert
       space. $)
    hsn0elch $p |- { 0h } e. CH $=
      ( vf vx vy c0v wcel cn cv chli wbr wa wal chil cva co csm ax-hv0cl pm3.2i
      wral cc wceq csn cch csh wf wi wss snssi ax-mp snid elsn oveq12 hvaddid2i
      elexi syl6eq ovex elsnc sylibr syl2anb rgen2a oveq2 sylan9eqr rgen2 issh2
      hvmul0 sylan2b mpbir2an cxp fconst2 hlim0 breq1 mpbiri sylbi vex hlimunii
      wb eleq1d sylan mpbii gen2 isch2 ) DUAZUBEWAUCEZFWAAGZUDZWCBGZHIZJZWEWAEZ
      UEZBKAKWBWALUFZDWAEZJWECGZMNZWAEZCWARBWARZWEWLONZWAEZCWARBSRZJWJWKDLEWJPD
      LUGUHDDLPUMZUIZQWOWRWNBCWAWHWEDTZWLDTZWNWLWAEZBDUJCDUJZXAXBJZWMDTWNXEWMDD
      MNDWEDWLDMUKDPULUNWMDWEWLMUOUPUQURUSWQBCSWAXCWESEZXBWQXDXFXBJWPDTWQXBXFWP
      WEDONDWLDWEOUTWEVDVAWPDWEWLOUOUPUQVEVBQBCWAVCVFWIABWGWKWHWTWDWCDHIZWFWKWH
      VOWDWCFWAVGZTZXGFDWCWSVHXIXGXHDHIVIWCXHDHVJVKVLXGWFJDWEWADWEWCWSBVMVNVPVQ
      VRVSBAWAVTVF $.
      $( [14-Oct-1999] $)
  $}

  $( From any nonzero Hilbert space vector, construct a vector whose norm is
     1. $)
  norm1 $p |- ( ( A e. ~H /\ A =/= 0h ) ->
               ( normh ` ( ( 1 / ( normh ` A ) ) .h A ) ) = 1 ) $=
    ( chil wcel wne wa c1 cno cfv co cmul cc wceq cr cc0 adantr syl2anc cle wbr
    recnd 1re c0v cdiv csm cabs normcl normne0 biimpar rereccl norm-iii normgt0
    simpl clt biimpa lt01 ltleii divge0 mpanl12 absid oveq1d mulcom recid eqtrd
    0re 3eqtrd ) ABCZAUADZEZFAGHZUBIZAUCIGHZVIUDHZVHJIZVIVHJIZFVGVIKCZVEVJVLLVG
    VIVGVHMCZVHNDZVIMCZVEVOVFAUEZOZVEVPVFAUFUGZVHUHPZSZVEVFUKVIAUIPVGVKVIVHJVGV
    QNVIQRZVKVILWAVGVONVHULRZWCVSVEVFWDAUJUMFMCNFQRVOWDEWCTNFVCTUNUOFVHUPUQPVIU
    RPUSVGVMVHVIJIZFVGVNVHKCZVMWELWBVEWFVFVEVHVRSOZVIVHUTPVGWFVPWEFLWGVTVHVAPVB
    VD $.
    $( [7-Feb-2006] $)

  ${
    $d x z H $.  $d y z H $.
    norm1ex.1 $e |- H e. SH $.
    $( A normalized vector exists in a subspace iff the subspace has a nonzero
       vector. $)
    norm1exi $p |- ( E. x e. H x =/= 0h <-> E. y e. H ( normh ` y ) = 1 ) $=
      ( vz cv c0v wne wrex cno cfv c1 wceq neeq1 cbvrexv wcel cc0 syl syl2anc
      co wa cdiv csm cc cr sheli normcl adantr wb normne0 biimpar rereccl recnd
      chil simpl csh shmulcl mp3an1 norm1 sylan fveq2 rcla4ev rexlimiva ax-1ne0
      eqeq1d wn df-ne mpbi eqeq1 mtbiri norm-i necon3bbid syl5ib reximia impbii
      sylib bitri ) AFZGHZACIEFZGHZECIZBFZJKZLMZBCIZVSWAAECVRVTGNOWBWFWAWFECVTC
      PZWAUAZLVTJKZUBTZVTUCTZCPZWKJKZLMZWFWHWJUDPZWGWLWHWJWHWIUEPZWIQHZWJUEPWGW
      PWAWGVTUNPZWPVTCDUFZVTUGRUHWGWQWAWGWRWQWAUIWSVTUJRUKWIULSUMWGWAUOCUPPWOWG
      WLDWJVTCUQURSWGWRWAWNWSVTUSUTWEWNBWKCWCWKMWDWMLWCWKJVAVEVBSVCWFWCGHZBCIWB
      WEWTBCWEWDQMZVFWCCPZWTWEXALQMZLQHXCVFVDLQVGVHWDLQVIVJXBXAWCGXBWCUNPXAWCGM
      UIWCCDUFWCVKRVLVMVNWTWABECWCVTGNOVPVOVQ $.
      $( [9-Apr-2006] $)
  $}

  ${
    $d x z $.  $d y z $.
    $( A normalized vector can exist only iff the Hilbert space has a nonzero
       vector. $)
    norm1hex $p |- ( E. x e. ~H x =/= 0h <-> E. y e. ~H ( normh ` y ) = 1 ) $=
      ( chil helsh norm1exi ) ABCDE $.
      $( [21-Jan-2006] $)
  $}

