$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Positive operators (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

    $( Ordering relation for positive operators.  Definition of positive
       operator ordering in [Kreyszig] p. 470. $)
    leopg $p |- ( ( T e. A /\ U e. B ) -> ( T <_op U <-> ( ( U -op T ) e.
                HrmOp /\ A. x e. ~H 0 <_ ( ( ( U -op T ) ` x ) .ih x ) ) ) ) $=
      ( vu vt cv chod co cho wcel cc0 cfv csp cle wbr chil wral wa oveq2 eleq1d
      cleo wceq fveq1d oveq1d breq2d ralbidv anbi12d oveq1 df-leop brabg ) FHZG
      HZIJZKLZMAHZUONZUQOJZPQZARSZTUMDIJZKLZMUQVBNZUQOJZPQZARSZTEDIJZKLZMUQVHNZ
      UQOJZPQZARSZTGFDEBCUCUNDUDZUPVCVAVGVNUOVBKUNDUMIUAZUBVNUTVFARVNUSVEMPVNUR
      VDUQOVNUQUOVBVOUEUFUGUHUIUMEUDZVCVIVGVMVPVBVHKUMEDIUJZUBVPVFVLARVPVEVKMPV
      PVDVJUQOVPUQVBVHVQUEUFUGUHUIAFGUKUL $.
      $( [23-Jul-2006] $)

    $( Ordering relation for operators.  Definition of positive operator
       ordering in [Kreyszig] p. 470. $)
    leop $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                A. x e. ~H 0 <_ ( ( ( U -op T ) ` x ) .ih x ) ) ) $=
      ( cho wcel wa cleo wbr chod co cc0 cv cfv csp cle chil leopg hmopd ancoms
      wral biantrurd bitr4d ) BDEZCDEZFZBCGHCBIJZDEZKALZUFMUHNJOHAPTZFUIADDBCQU
      EUGUIUDUCUGCBRSUAUB $.
      $( [23-Jul-2006] $)

    $( Ordering relation for operators.  Definition of operator ordering in
       [Young] p. 141. $)
    leop2 $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                A. x e. ~H ( ( T ` x ) .ih x ) <_ ( ( U ` x ) .ih x ) ) ) $=
      ( cho wcel wa wbr cc0 co cfv csp cle chil wral wceq hmopf ffvelrn adantll
      wf adantlr cleo cv chod leop anim12i cmv hodval 3com12 3expa oveq1d simpr
      cmin his2sub syl3anc eqtrd sylan breq2d cr wb hmopre subge0 syl2anc bitrd
      ralbidva ) BDEZCDEZFZBCUAGHAUBZCBUCIJZVHKIZLGZAMNVHBJZVHKIZVHCJZVHKIZLGZA
      MNABCUDVGVKVPAMVGVHMEZFZVKHVOVMULIZLGZVPVRVJVSHLVGMMBSZMMCSZFZVQVJVSOVEWA
      VFWBBPCPUEWCVQFZVJVNVLUFIZVHKIZVSWDVIWEVHKWAWBVQVIWEOZWBWAVQWGVHCBUGUHUIU
      JWDVNMEZVLMEZVQWFVSOWBVQWHWAMMVHCQRWAVQWIWBMMVHBQTWCVQUKVNVLVHUMUNUOUPUQV
      RVOUREZVMUREZVTVPUSVFVQWJVEVHCUTRVEVQWKVFVHBUTTVOVMVAVBVCVDVC $.
      $( [23-Jul-2006] $)

    $( Operator ordering in terms of a positive operator.  Definition of
       operator ordering in [Retherford] p. 49. $)
    leop3 $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                0hop <_op ( U -op T ) ) ) $=
      ( vx cho wcel wa cleo wbr cc0 cv chod cfv csp cle chil wral ch0o leop c0v
      co wb 0hmop hmopd ancoms leop2 sylancr ho0val oveq1d eqtrd breq1d ralbiia
      hi01 syl6rbb bitrd ) ADEZBDEZFZABGHICJZBAKTZLURMTZNHZCOPZQUSGHZCABRUQVCUR
      QLZURMTZUTNHZCOPZVBUQQDEUSDEZVCVGUAUBUPUOVHBAUCUDCQUSUEUFVFVACOUROEZVEIUT
      NVIVESURMTIVIVDSURMURUGUHURULUIUJUKUMUN $.
      $( [23-Jul-2006] $)

    $( Binary relation defining a positive operator.  Definition VI.1 of
       [Retherford] p. 49. $)
    leoppos $p |- ( T e. HrmOp -> ( 0hop <_op T <->
                A. x e. ~H 0 <_ ( ( T ` x ) .ih x ) ) ) $=
      ( cho wcel ch0o cleo wbr cc0 cv chod co cfv csp chil wral 0hmop leop mpan
      cle wb wf wceq hmopf hosubid1 syl fveq1d oveq1d breq2d ralbidv bitrd ) BC
      DZEBFGZHAIZBEJKZLZUMMKZSGZANOZHUMBLZUMMKZSGZANOECDUKULURTPAEBQRUKUQVAANUK
      UPUTHSUKUOUSUMMUKUMUNBUKNNBUAUNBUBBUCBUDUEUFUGUHUIUJ $.
      $( [25-Jul-2006] $)

    $( The ordering relation for operators is reflexive. $)
    leoprf2 $p |- ( T : ~H --> ~H -> T <_op T ) $=
      ( vx chil wf cleo wbr chod co cho wcel cc0 cfv csp cle ch0o wa c0v adantl
      wceq cvv hodid 0hmop syl6eqel 0re leidi adantr fveq1d ho0val eqtrd oveq1d
      cv wral hi01 eqtr2d syl5breq ralrimiva ax-hilex fex mpan2 leopg mpbir2and
      wb syl2anc ) CCADZAAEFZAAGHZIJZKBUKZVFLZVHMHZNFZBCULZVDVFOIAUAZUBUCVDVKBC
      VDVHCJZPZKKVJNKUDUEVOVJQVHMHZKVOVIQVHMVOVIVHOLZQVOVHVFOVDVFOSVNVMUFUGVNVQ
      QSVDVHUHRUIUJVNVPKSVDVHUMRUNUOUPVDATJZVRVEVGVLPVBVDCTJVRUQCCTAURUSZVSBTTA
      AUTVCVA $.
      $( [24-Jul-2006] $)

    $( The ordering relation for operators is reflexive. $)
    leoprf $p |- ( T e. HrmOp -> T <_op T ) $=
      ( vx cho wcel cleo wbr cc0 cv chod co cfv csp cle chil wral c0v ch0o wceq
      wa adantl 0re leidi wf hmopf hodid adantr fveq1d ho0val eqtrd oveq1d hi01
      syl eqtr2d syl5breq ralrimiva wb leop anidms mpbird ) ACDZAAEFZGBHZAAIJZK
      ZVBLJZMFZBNOZUTVFBNUTVBNDZSZGGVEMGUAUBVIVEPVBLJZGVIVDPVBLVIVDVBQKZPVIVBVC
      QUTVCQRZVHUTNNAUCVLAUDAUEULUFUGVHVKPRUTVBUHTUIUJVHVJGRUTVBUKTUMUNUOUTVAVG
      UPBAAUQURUS $.
      $( [23-Jul-2006] $)

    $( The square of a Hermitian operator is positive. $)
    leopsq $p |- ( T e. HrmOp -> 0hop <_op ( T o. T ) ) $=
      ( vx cho wcel ch0o ccom cleo wbr cc0 cv cfv csp co cle chil wral wa sylan
      syl wceq wf hmopf ffvelrn hiidge0 simpl simpr syl3anc fvco3 oveq1d eqtr4d
      hmop breqtrd ralrimiva wb eqid hmopco mp3an3 anidms leoppos mpbird ) ACDZ
      EAAFZGHZIBJZVBKZVDLMZNHZBOPZVAVGBOVAVDODZQZIVDAKZVKLMZVFNVJVKODZIVLNHVAOO
      AUAZVIVMAUBZOOVDAUCRZVKUDSVJVLVKAKZVDLMZVFVJVAVMVIVLVRTVAVIUEVPVAVIUFVKVD
      AUKUGVJVEVQVDLVAVNVIVEVQTVOOOVDAAUHRUIUJULUMVAVBCDZVCVHUNVAVSVAVAVBVBTVSV
      BUOAAUPUQURBVBUSSUT $.
      $( [23-Aug-2006] $)

    $( The zero operator is a positive operator.  (The literature calls it
       "positive," even though in some sense it is really "nonnegative.") Part
       of Example 12.2(i) in [Young] p. 142. $)
    0leop $p |- 0hop <_op 0hop $=
      ( ch0o cho wcel cleo wbr 0hmop leoprf ax-mp ) ABCAADEFAGH $.
      $( [23-Jul-2006] $)

    $( The identity operator is a positive operator.  Part of Example 12.2(i)
       in [Young] p. 142. $)
    idleop $p |- 0hop <_op Iop $=
      ( vx ch0o chio cleo wbr cv cfv csp co cle chil cho wcel wral 0hmop idhmop
      wb cc0 c0v oveq1d leop2 mp2an hiidge0 ho0val eqtrd hoival 3brtr4d mprgbir
      hi01 ) BCDEZAFZBGZUKHIZUKCGZUKHIZJEZAKBLMCLMUJUPAKNQOPABCUAUBUKKMZRUKUKHI
      UMUOJUKUCUQUMSUKHIRUQULSUKHUKUDTUKUIUEUQUNUKUKHUKUFTUGUH $.
      $( [23-Jul-2006] $)

    $( The sum of two positive operators is positive.  Exercise 1(i) of
       [Retherford] p. 49. $)
    leopadd $p |- ( ( ( T e. HrmOp /\ U e. HrmOp ) /\ ( 0hop <_op T
               /\ 0hop <_op U ) ) -> 0hop <_op ( T +op U ) ) $=
      ( vx cho wcel wa ch0o cleo wbr co cc0 cfv csp chil wral cr hmopre leoppos
      cle wceq chos cv r19.26 caddc wi addge0 ex syl2an anandirs wf anim12i cva
      hmopf w3a hosval oveq1d 3expa ffvelrn adantlr adantll simpr ax-his2 eqtrd
      syl3anc sylan breq2d sylibrd ralimdva syl5bir bi2anan9 wb syl 3imtr4d imp
      hmops ) ADEZBDEZFZGAHIZGBHIZFZGABUAJZHIZVRKCUBZALZWDMJZSIZCNOZKWDBLZWDMJZ
      SIZCNOZFZKWDWBLZWDMJZSIZCNOZWAWCWMWGWKFZCNOVRWQWGWKCNUCVRWRWPCNVRWDNEZFZW
      RKWFWJUDJZSIZWPVPVQWSWRXBUEZVPWSFWFPEZWJPEZXCVQWSFWDAQWDBQXDXEFWRXBWFWJUF
      UGUHUIWTWOXAKSVRNNAUJZNNBUJZFZWSWOXATVPXFVQXGAUMBUMUKXHWSFZWOWEWIULJZWDMJ
      ZXAXFXGWSWOXKTXFXGWSUNWNXJWDMWDABUOUPUQXIWENEZWINEZWSXKXATXFWSXLXGNNWDAUR
      USXGWSXMXFNNWDBURUTXHWSVAWEWIWDVBVDVCVEVFVGVHVIVPVSWHVQVTWLCARCBRVJVRWBDE
      WCWQVKABVOCWBRVLVMVN $.
      $( [25-Jul-2006] $)

    $( The scalar product of a nonnegative real and a positive operator is a
       positive operator.  Exercise 1(ii) of [Retherford] p. 49. $)
    leopmuli $p |- ( ( ( A e. RR /\ T e. HrmOp ) /\ ( 0 <_ A /\
                0hop <_op T ) ) -> 0hop <_op ( A .op T ) ) $=
      ( vx cr wcel cho wa cc0 cle wbr ch0o cleo co cfv csp chil wral wb leoppos
      wceq chot cv cmul wi hmopre mulge0 sylanr1 expr an4s anassrs recn anim12i
      cc wf hmopf csm homval 3expa oveq1d ffvelrn adantll simpr ax-his3 syl3anc
      simpll eqtrd sylan breq2d adantlr sylibrd expimpd adantl anbi2d hmopm syl
      ralimdva 3imtr4d imp ) ADEZBFEZGZHAIJZKBLJZGZKABUAMZLJZWAWBHCUBZBNZWGOMZI
      JZCPQZGHWGWENZWGOMZIJZCPQZWDWFWAWBWKWOWAWBGZWJWNCPWPWGPEZGWJHAWIUCMZIJZWN
      WAWBWQWJWSUDZVSWBVTWQWTVSWBGZVTWQGZWJWSXBXAWIDEWJWSWGBUEAWIUFUGUHUIUJWAWQ
      WNWSRWBWAWQGWMWRHIWAAUMEZPPBUNZGZWQWMWRTVSXCVTXDAUKBUOULXEWQGZWMAWHUPMZWG
      OMZWRXFWLXGWGOXCXDWQWLXGTAWGBUQURUSXFXCWHPEZWQXHWRTXCXDWQVEXDWQXIXCPPWGBU
      TVAXEWQVBAWHWGVCVDVFVGVHVIVJVPVKWAWCWKWBVTWCWKRVSCBSVLVMWAWEFEWFWORABVNCW
      ESVOVQVR $.
      $( [25-Jul-2006] $)

    $( The scalar product of a positive real and a positive operator is a
       positive operator.  Exercise 1(ii) of [Retherford] p. 49. $)
    leopmul $p |- ( ( A e. RR /\ T e. HrmOp /\ 0 < A ) ->
                ( 0hop <_op T <-> 0hop <_op ( A .op T ) ) ) $=
      ( cr wcel cho cc0 clt wbr ch0o cleo chot co wa cle adantr 3adant2 syl2anc
      0re c1 wceq w3a 3simpa ltle 3impia mp3an1 anim1i leopmuli cdiv wne gt0ne0
      rereccl syldan hmopm 3adant3 recgt0 wi sylancr mpd jca31 anassrs sylan cc
      cmul recn recid2 oveq1d wf reccl 3ad2ant1 hmopf 3ad2ant2 homulass syl3anc
      chil homulid2 syl 3eqtr3d breqtrd impbida ) ACDZBEDZFAGHZUAZIBJHZIABKLZJH
      ZWCWDMVTWAMZFANHZWDMWFWCWGWDVTWAWBUBOWCWHWDVTWBWHWAFCDZVTWBWHRWIVTWBWHFAU
      CUDUEPUFABUGQWCWFMISAUHLZWEKLZBJWCWJCDZWEEDZMZFWJNHZMWFIWKJHZWCWLWMWOVTWB
      WLWAVTWBAFUIZWLAUJZAUKULZPVTWAWMWBABUMUNVTWBWOWAVTWBMZFWJGHZWOAUOWTWIWLXA
      WOUPRWSFWJUCUQURPUSWNWOWFWPWJWEUGUTVAWCWKBTWFWCWJAVCLZBKLZSBKLZWKBVTWBXCX
      DTWAWTXBSBKWTAVBDZWQXBSTVTXEWBAVDZOZWRAVEQVFPWCWJVBDZXEVNVNBVGZXCWKTVTWBX
      HWAWTXEWQXHXGWRAVHQPVTWAXEWBXFVIWAVTXIWBBVJZVKWJABVLVMWAVTXDBTZWBWAXIXKXJ
      BVOVPVKVQOVRVS $.
      $( [23-Aug-2006] $)

    $( Scalar product applied to operator ordering. $)
    leopmul2i $p |- ( ( ( A e. RR /\ T e. HrmOp /\ U e. HrmOp ) /\ ( 0 <_ A /\
                T <_op U ) ) -> ( A .op T ) <_op ( A .op U ) ) $=
      ( wcel cho wbr cleo chot co wa ch0o chod wi 3adant1 wb leop3 adantr hmopm
      chil wf w3a cc0 cle simp1 hmopd ancoms leopmuli syl2anc imp syl2an 3impdi
      cr exp32 wceq recn hmopf hosubdi syl3an 3com23 breq2d bitr4d 3imtr4d impr
      cc ) AULDZBEDZCEDZUAZUBAUCFZBCGFZABHIZACHIZGFZVHVIJKCBLIZGFZKAVNHIZGFZVJV
      MVHVIVOVQMZVHVEVNEDZVIVRMVEVFVGUDVFVGVSVEVGVFVSCBUEUFNVEVSJVIVOVQAVNUGUMU
      HUIVHVJVOOZVIVFVGVTVEBCPNQVHVMVQOVIVHVMKVLVKLIZGFZVQVEVFVGVMWBOZVEVFJVKED
      VLEDWCVEVGJABRACRVKVLPUJUKVHVPWAKGVEVGVFVPWAUNZVEAVDDVGSSCTVFSSBTWDAUOCUP
      BUPACBUQURUSUTVAQVBVC $.
      $( [12-Aug-2006] $)

    $( The positive operator ordering relation satisfies trichotomy.  Exercise
       1(iii) of [Retherford] p. 49. $)
    leoptri $p |- ( ( T e. HrmOp /\ U e. HrmOp ) ->
           ( ( T <_op U /\ U <_op T ) <-> T = U ) ) $=
      ( vx cho wcel wa cleo wbr cfv csp co cle chil wral leop2 wb cr hmopre clo
      wceq ancoms anbi12d adantlr adantll letri3 syl2anc r19.26 syl6rbb hmoplin
      cv ralbidva lnopeq syl2an 3bitrd ) ADEZBDEZFZABGHZBAGHZFCUJZAIUTJKZUTBIUT
      JKZLHZCMNZVBVALHZCMNZFZVAVBTZCMNZABTZUQURVDUSVFCABOUPUOUSVFPCBAOUAUBUQVIV
      CVEFZCMNVGUQVHVKCMUQUTMEZFVAQEZVBQEZVHVKPUOVLVMUPUTARUCUPVLVNUOUTBRUDVAVB
      UEUFUKVCVECMUGUHUOASEBSEVIVJPUPAUIBUICABULUMUN $.
      $( [23-Apr-2008] $) $( [25-Jul-2006] $)

    $( The positive operator ordering relation is transitive.  Exercise 1(iv)
       of [Retherford] p. 49. $)
    leoptr $p |- ( ( ( S e. HrmOp /\ T e. HrmOp /\ U e. HrmOp ) /\
           ( S <_op T /\ T <_op U ) ) -> S <_op U ) $=
      ( vx cho wcel w3a cleo wbr wa cfv csp co cle chil wral cr hmopre wb leop2
      cv r19.26 wi letr syl3an ralimdva syl5bir 3adant3 3adant1 anbi12d 3adant2
      3anandirs 3imtr4d imp ) AEFZBEFZCEFZGZABHIZBCHIZJZACHIZURDUAZAKVCLMZVCBKV
      CLMZNIZDOPZVEVCCKVCLMZNIZDOPZJZVDVHNIZDOPZVAVBVKVFVIJZDOPURVMVFVIDOUBURVN
      VLDOUOUPUQVCOFZVNVLUCZUOVOJVDQFUPVOJVEQFUQVOJVHQFVPVCARVCBRVCCRVDVEVHUDUE
      ULUFUGURUSVGUTVJUOUPUSVGSUQDABTUHUPUQUTVJSUODBCTUIUJUOUQVBVMSUPDACTUKUMUN
      $.
      $( [5-Jul-2008] $) $( [25-Jul-2006] $)

    $( A bounded Hermitian operator is less than or equal to its norm times the
       identity operator. $)
    leopnmid $p |- ( ( T e. HrmOp /\ ( normop ` T ) e. RR ) ->
                    T <_op ( ( normop ` T ) .op Iop ) ) $=
      ( vx cho wcel cfv cr wa chio co wbr csp cle chil adantlr syl sylan adantl
      cc cmul wceq cnop chot cleo cv wral hmopre recnd abscl idhmop hmopm mpan2
      cabs adantll leabs cno wf ffvelrn normcl remulcl syl2anc bcs sylancom cc0
      hmopf sylan2 normge0 jca cbo clo hmoplin elbdop2 biimpri nmbdoplb lemul1a
      syl31anc csm cexp sqval normsq eqtr3d oveq2d recn ad2antlr mulass syl3anc
      c2 simpr ax-his3 3eqtr4d wf1o hoif f1of homval hoival eqtrd oveq1d eqtr4d
      mp1i breqtrd letrd ralrimiva wb leop2 mpbird ) ACDZAUAEZFDZGZAXFHUBIZUCJZ
      BUDZAEZXKKIZXKXIEZXKKIZLJZBMUEZXHXPBMXHXKMDZGZXMXMULEZXOXEXRXMFDZXGXKAUFZ
      NXEXRXTFDZXGXEXRGZXMRDYCYDXMYBUGXMUHONZXGXRXOFDZXEXGXICDZXRYFXGHCDYGUIXFH
      UJUKZXKXIUFPUMZXEXRXMXTLJZXGYDYAYJYBXMUNONXSXTXLUOEZXKUOEZSIZXOYEXSYKFDZY
      LFDZYMFDXEXRYNXGXEMMAUPZXRYNAVDZYPXRGXLMDZYNMMXKAUQZXLUROPNZXRYOXHXKURZQZ
      YKYLUSUTYIXEXRXTYMLJZXGXEXRYRUUCXEYPXRYRYQYSPXLXKVAVBNXSYMXFYLSIZYLSIZXOL
      XSYNUUDFDZYOVCYLLJZGZYKUUDLJZYMUUELJYTXGXRUUFXEXRXGYOUUFUUAXFYLUSVEUMXRUU
      HXHXRYOUUGUUAXKVFVGQXHAVHDZXRUUIXEAVIDZXGUUJAVJUUJUUKXGGAVKVLPXKAVMPYKUUD
      YLVNVOXSUUEXFXKVPIZXKKIZXOXSXFYLYLSIZSIZXFXKXKKIZSIZUUEUUMXRUUOUUQTXHXRUU
      NUUPXFSXRYLWFVQIZUUNUUPXRYLRDZUURUUNTXRYLUUAUGYLVROXKVSVTWAQXSXFRDZUUSUUS
      UUEUUOTXGUUTXEXRXFWBWCZXSYLUUBUGZUVBXFYLYLWDWEXSUUTXRXRUUMUUQTUVAXHXRWGZU
      VCXFXKXKWHWEWIXSXNUULXKKXSXNXFXKHEZVPIZUULXSUUTMMHUPZXRXNUVETUVAMMHWJUVFX
      SWKMMHWLWRUVCXFXKHWMWEXRUVEUULTXHXRUVDXKXFVPXKWNWAQWOWPWQWSWTWTXAXGXEYGXJ
      XQXBYHBAXIXCVEXD $.
      $( [11-Aug-2006] $)

    $( A nonzero, bounded Hermitian operator divided by its norm is less than
       or equal to the identity operator. $)
    nmopleid $p |- ( ( T e. HrmOp /\ ( normop ` T ) e. RR /\ T =/= 0hop ) ->
                    ( ( 1 / ( normop ` T ) ) .op T ) <_op Iop ) $=
      ( cho wcel cr wne c1 co chot chio cleo wbr cc0 sylan adantlr adantll chil
      wa clt wf wceq cnop cfv ch0o clo hmoplin nmlnopne0 biimpar rereccl simpll
      cdiv cle idhmop hmopm mpan2 ad2antlr simplr nmopgt0 biimpa recgt0 syl2anc
      hmopf wi 0re ltle sylancr leopnmid adantr leopmul2i syl32anc cc recn cmul
      mpd reccl simpl wf1o hoif f1of ax-mp homulass mp3an3 recid2 oveq1d eqtr3d
      homulid2 syl6eq breqtrd syldan 3impa ) ABCZAUAUBZDCZAUCEZFWKUJGZAHGZIJKZW
      JWLQZWMWKLEZWPWJWMWRWLWJAUDCZWMWRAUEWSWRWMAUFUGMNWQWRQZWOWNWKIHGZHGZIJWTW
      NDCZWJXABCZLWNUKKZAXAJKZWOXBJKWLWRXCWJWKUHZOWJWLWRUIWLXDWJWRWLIBCXDULWKIU
      MUNUOWTLWNRKZXEWTWLLWKRKZXHWJWLWRUPWJWRXIWLWJPPASZWRXIAVAXJWRXIAUQURMNWKU
      SUTWLWRXHXEVBZWJWLWRQLDCXCXKVCXGLWNVDVEOVMWQXFWRAVFVGWNAXAVHVIWLWRXBITZWJ
      WLWKVJCZWRXLWKVKXMWRQZXBFIHGZIXNWNWKVLGZIHGZXBXOXNWNVJCZXMXQXBTZWKVNXMWRV
      OXRXMPPISZXSPPIVPXTVQPPIVRVSZWNWKIVTWAUTXNXPFIHWKWBWCWDXTXOITYAIWEVSWFMOW
      GWHWI $.
      $( [12-Aug-2006] $)
  $}

  ${
    $d u v T $.
    opsqrlem1.1 $e |- T e. HrmOp $.
    opsqrlem1.2 $e |- ( normop ` T ) e. RR $.
    opsqrlem1.3 $e |- 0hop <_op T $.
    opsqrlem1.4 $e |- R = ( ( 1 / ( normop ` T ) ) .op T ) $.
    opsqrlem1.5 $e |- ( T =/= 0hop -> E. u e. HrmOp ( 0hop <_op u /\
                      ( u o. u ) = R ) ) $.
    $( Lemma for opsqri . $)
    opsqrlem1 $p |- ( T =/= 0hop ->
                   E. v e. HrmOp ( 0hop <_op v /\ ( v o. v ) = T ) ) $=
      ( ch0o wbr ccom wceq wa cho wcel chot co chil ax-mp wne cv cleo wrex cnop
      cfv csqr cr cc0 cle wf hmopf nmopge0 hmopm mpan ad2antlr sqrge0i leopmuli
      sqrcli mpanr1 mpanl1 ad2ant2lr cc recni homulcl syl homco1 mp3an1 syl2anc
      clo hmoplin homco2 oveq2d cmul sqrthi oveq1i fco homulass syl5reqr 3eqtrd
      mp3an12 c1 cdiv id syl6eq nmlnopne0 recidzi sylbir oveq1d rerecclzi recnd
      mp3an13 homulid2 mp1i 3eqtr3d sylan9eqr adantlr eqtrd adantrl breq2 coeq1
      wb coeq2 eqeq1d anbi12d rcla4ev syl12anc exp31 rexlimdv mpd ) DJUAZJBUBZU
      CKZXLXLLZCMZNZBOUDJAUBZUCKZXQXQLZDMZNZAOUDZIXKXPYBBOXKXLOPZXPYBXKYCNZXPND
      UEUFZUGUFZXLQRZOPZJYGUCKZYGYGLZDMZYBYCYHXKXPYFUHPZYCYHUIYEUJKZYLSSDUKZYMD
      OPZYNEDULTZDUMTZYEFUSTZYFXLUNUOUPYCXMYIXKXOYLYCXMYIYRYLYCNUIYFUJKZXMYIYMY
      SYQYEFUQTYFXLURUTVAVBYDXOYKXMYDXONYJYEXNQRZDYCYJYTMXKXOYCYJYFXLYGLZQRZYFY
      FXNQRZQRZYTYCSSXLUKZSSYGUKZYJUUBMZXLULZYCUUEUUFUUHYFVCPZUUEUUFYFYRVDZYFXL
      VEUOVFUUIUUEUUFUUGUUJYFXLYGVGVHVIYCUUAUUCYFQYCXLVJPZUUEUUAUUCMZXLVKUUHUUI
      UUKUUEUULUUJYFXLXLVLVHVIVMYCYTYFYFVNRZXNQRZUUDUUMYEXNQYMUUMYEMYQYEFVOTVPY
      CSSXNUKZUUNUUDMZYCUUEUUEUUOUUHUUHSSSXLXLVQVIUUIUUIUUOUUPUUJUUJYFYFXNVRWAV
      FVSVTUPXKXOYTDMYCXOXKYTYEWBYEWCRZDQRZQRZDXOXNUURYEQXOXNCUURXOWDHWEVMXKYEU
      UQVNRZDQRZWBDQRZUUSDXKUUTWBDQXKYEUIUAZUUTWBMDVJPZUVCXKXBYOUVDEDVKTDWFTZYE
      YEFVDZWGWHWIXKUUQVCPZUVAUUSMZXKUUQXKUVCUUQUHPUVEYEFWJWHWKYEVCPUVGYNUVHUVF
      YPYEUUQDVRWLVFYNUVBDMXKYPDWMWNWOWPWQWRWSYAYIYKNAYGOXQYGMZXRYIXTYKXQYGJUCW
      TUVIXSYJDUVIXSYGXQLYJXQYGXQXAXQYGYGXCWRXDXEXFXGXHXIXJ $.
      $( [28-Jul-2011] $) $( [9-Aug-2006] $)
  $}

  ${
    $d j k F $.  $d w z G $.  $d j k N $.  $d w z S $.  $d u v w x y z T $.
    $d w z H $.
    opsqrlem2.1 $e |- T e. HrmOp $.
    opsqrlem2.2 $e |- S = ( x e. HrmOp , y e. HrmOp |->
          ( x +op ( ( 1 / 2 ) .op ( T -op ( x o. x ) ) ) ) ) $.
    opsqrlem2.3 $e |- F = seq 1 ( S , ( NN X. { 0hop } ) ) $.
    $( Lemma for opsqri . ` F `` N ` is the recursive function A_n (starting at
       n=1 instead of 0) of Theorem 9.4-2 of [Kreyszig] p. 476. $)
    opsqrlem2 $p |- ( F ` 1 ) = 0hop $=
      ( c1 cfv cn ch0o csn cxp cseq fveq1i cz wcel wceq ax-mp 1z seq1 1nn 0hmop
      cho elexi fvconst2 3eqtri ) IEJICKLMNZIOZJZIUIJZLIEUJHPIQRUKULSUACUIIUBTI
      KRULLSUCKLILUEUDUFUGTUH $.
      $( [28-Jul-2011] $) $( [17-Aug-2006] $)

    $( Lemma for opsqri . $)
    opsqrlem3 $p |- ( ( G e. HrmOp /\ H e. HrmOp ) ->
              ( G S H ) = ( G +op ( ( 1 / 2 ) .op ( T -op ( G o. G ) ) ) ) ) $=
      ( vz vw cho cv co chod chot chos wceq oveq2d c1 cdiv ccom coeq12d oveq12d
      c2 id eqidd cmpt2 cbvmpt2v eqtri ovex ovmpt2 ) KLFGMMKNZUAUFUBOZDUNUNUCZP
      OZQOZROZFUODFFUCZPOZQOZROZCVCUNFSZUNFURVBRVDUGZVDUQVAUOQVDUPUTDPVDUNFUNFV
      EVEUDTTUELNZGSVCUHCABMMANZUODVGVGUCZPOZQOZROZUIKLMMUSUIIABKLMMVKUSUSVGUNS
      ZVGUNVJURRVLUGZVLVIUQUOQVLVHUPDPVLVGUNVGUNVMVMUDTTUEBNVFSUSUHUJUKFVBRULUM
      $.
      $( [28-Jul-2011] $) $( [22-Aug-2006] $)

    $( Lemma for opsqri . $)
    opsqrlem4 $p |- F : NN --> HrmOp $=
      ( vz vw cn cho wf ch0o c1 wtru wcel cv 0hmop co csn cxp cseq nnuz a1i cfv
      cz 1z elexi fvconst2 syl6eqel wa c2 cdiv ccom chod chot chos opsqrlem3 cr
      adantl rehalfcl ax-mp wceq simpl eqidd hmopco syl3anc hmopd sylancr hmopm
      1re hmops syldan eqeltrd seqf trud feq1i mpbir ) KLEMKLCKNUAUBZOUCZMZWBPI
      JCLVTOKUDOUGQPUHUEIRZKQZWCVTUFZLQPWDWENLKNWCNLSUIUJSUKVAWCLQZJRZLQZULZWCW
      GCTZLQPWIWJWCOUMUNTZDWCWCUOZUPTZUQTZURTZLABCDEWCWGFGHUSWFWHWNLQZWOLQWIWKU
      TQZWMLQZWPOUTQWQVLOVBVCWIDLQWLLQZWRFWIWFWFWLWLVDWSWFWHVEZWTWIWLVFWCWCVGVH
      DWLVIVJWKWMVKVJWCWNVMVNVOVAVPVQKLEWAHVRVS $.
      $( [28-Jul-2011] $) $( [17-Aug-2006] $)

    $( Lemma for opsqri . $)
    opsqrlem5 $p |- ( N e. NN -> ( F ` ( N + 1 ) ) =
  ( ( F ` N ) +op ( ( 1 / 2 ) .op ( T -op ( ( F ` N ) o. ( F ` N ) ) ) ) ) ) $=
      ( cn wcel c1 caddc co cfv ch0o wceq fveq1i cho 0hmop csn cxp c2 cdiv ccom
      chod chot chos cseq elnnuz seqp1 sylbi oveq1i opsqrlem4 ffvelrni peano2nn
      cuz 3eqtr4g elexi fvconst2 syl syl6eqel opsqrlem3 syl2anc eqtrd ) FJKZFLM
      NZEOZFEOZVGJPUAUBZOZCNZVILUCUDNDVIVIUEUFNUGNUHNZVFVGCVJLUIZOZFVNOZVKCNZVH
      VLVFFLUQOKVOVQQFUJCVJLFUKULVGEVNIRVIVPVKCFEVNIRUMURVFVISKVKSKVLVMQJSFEABC
      DEGHIUNUOVFVKPSVFVGJKVKPQFUPJPVGPSTUSUTVATVBABCDEVIVKGHIVCVDVE $.
      $( [28-Jul-2011] $) $( [17-Aug-2006] $)

    ${
      opsqrlem6.4 $e |- T <_op Iop $.
      $( Lemma for opsqri . $)
      opsqrlem6 $p |- ( N e. NN -> ( F ` N ) <_op Iop ) $=
        ( chio cleo co wceq wcel chod c2 chos syl2anc chil vj vk cv caddc fveq2
        cfv wbr c1 breq1d ch0o opsqrlem2 idleop eqbrtri chot ccom cho opsqrlem4
        cn ffvelrni idhmop hmopd mpan syl eqid hmopco mp3an3 leopsq leop3 mp2an
        3syl wb mpbi wa leopadd mpanl2 mpanr2 wf hmopf fco ax-mp hosubcl cc 2cn
        cdiv 2ne0 reccli homulcl hoadddi mp3an1 homulass recidi oveq1i homulid2
        mp3an12 syl5eq eqtr3d oveq2d eqtrd hoaddcl hosubdi ho2times hoaddsubass
        a1i hoadd32 mp3an13 3eqtr4rd oveq1d mp3an23 hosubsub4 3eqtr3d hosubadd4
        mpanr1 mpanl1 3eqtr2d hocsubdir clo hmoplin hoddi hoid1i hoico2 oveq12d
        cmul mp3an2 hoico1 jctil syl12anc opsqrlem5 3eqtr4d breqtrd peano2nn cr
        3eqtrd cc0 clt 2re 2pos leopmul mpbird mpan2 nn1suc ) UAUCZEUFZKLUGUHEU
        FZKLUGUBUCZUHUDMZEUFZKLUGZFEUFZKLUGUAUBFUUAUHNUUBUUCKLUUAUHEUEUIUUAUUEN
        UUBUUFKLUUAUUEEUEUIUUAFNUUBUUHKLUUAFEUEUIUUCUJKLABCDEGHIUKULUMUUDUROZUU
        GUJKUUFPMZLUGZUUIUUKUJQUUJUNMZLUGZUUIUJKUUDEUFZPMZUUOUOZKDPMZRMZUULLUUI
        UUPUPOZUJUUPLUGZUJUURLUGZUUIUUOUPOZUVBUUSUUIUUNUPOZUVBURUPUUDEABCDEGHIU
        QZUSZKUPOZUVCUVBUTKUUNVAVBZVCZUVHUVBUVBUUPUUPNUUSUUPVDUUOUUOVEVFSUUIUVC
        UVBUUTUVEUVGUUOVGVJUUSUUTUJUUQLUGZUVADKLUGZUVIJDUPOZUVFUVJUVIVKGUTDKVHV
        IVLUUSUUQUPOZUUTUVIVMUVAUVFUVKUVLUTGKDVAVIUUPUUQVNVOVPSUUIKUUNUUNUOZQUU
        NUNMZPMZRMZUUQRMZQKUUNUHQWDMZDUVMPMZUNMZRMZPMZUNMZUURUULUUIQKUNMZQUWAUN
        MZPMZUWDUVNUVSRMZPMZUWCUVQUUIUWEUWGUWDPUUIUWEUVNQUVTUNMZRMZUWGUUITTUUNV
        QZTTUVTVQZUWEUWJNZUUIUVCUWKUVEUUNVRZVCZUUITTUVMVQZTTUVSVQZUWLUUIUWKUWKU
        WPUWOUWOTTTUUNUUNVSSZTTDVQZUWPUWQUVKUWSGDVRVTZDUVMWAVBZUVRWBOZUWQUWLQWC
        WEWFZUVRUVSWGVBVJZQWBOZUWKUWLUWMWCQUUNUVTWHWISUUIUWIUVSUVNRUUIQUVRYBMZU
        VSUNMZUWIUVSUUIUWPUWQUXGUWINZUWRUXAUXEUXBUWQUXHWCUXCQUVRUVSWJWNVJUUIUXG
        UHUVSUNMZUVSUXFUHUVSUNQWCWEWKWLUUIUWPUWQUXIUVSNUWRUXAUVSWMVJWOWPWQWRWQU
        UITTUWAVQZUWCUWFNZUUIUWKUWLUXJUWOUXDUUNUVTWSSUXETTKVQZUXJUXKWCUVFUXLUTK
        VRVTZQKUWAWTWNVCUUIUVQUWDUVMRMZUVNDRMPMZUWDUVNPMUVSPMZUWHUUIUVPKRMZDPMZ
        UXNUVNPMZDPMZUVQUXOUUIUXQUXSDPUUIUWDUVORMZKKRMZUVORMZUXSUXQUYAUYCNUUIUW
        DUYBUVORUXLUWDUYBNUXMKXAVTWLXCUUIUWPTTUVNVQZUXSUYANZUWRUUIUVCUWKUYDUVEU
        WNUXEUWKUYDWCQUUNWGVBVJZTTUWDVQZUWPUYDUYEUXEUXLUYGWCUXMQKWGVIZUWDUVMUVN
        XBWISUUITTUVOVQZUXQUYCNZUUIUWPUYDUYIUWRUYFUVMUVNWASZUXLUYIUXLUYJUXMUXMK
        UVOKXDXEVCXFXGUUIUYITTUVPVQZUXRUVQNZUYKUXLUYIUYLUXMKUVOWSVBUYLUXLUWSUYM
        UXMUWTUVPKDXBXHVJUUITTUXNVQZUYDUXTUXONZUUIUWPUYNUWRUYGUWPUYNUYHUWDUVMWS
        VBVCUYFUYNUYDUWSUYOUWTUXNUVNDXIVFSXJUUIUYDUWPUXPUXONZUYFUWRUYGUYDUWPUYP
        UYHUYGUYDVMUWSUWPUYPUWTUWDUVNDUVMXKXLXMSUUIUYDUWQUXPUWHNZUYFUUIUWPUWQUW
        RUXAVCUYGUYDUWQUYQUYHUWDUVNUVSXIWISXNXFUUIUUPUVPUUQRUUIUUPKUUOUOZUUNUUO
        UOZPMZKUVMRMZUUNUUNRMZPMZUVPUUIUWKTTUUOVQZUUPUYTNZUWOUUIUVCUWKVUDUVEUWN
        UXLUWKVUDUXMKUUNWAVBVJUXLUWKVUDVUEUXMKUUNUUOXOWISUUIUYTUUOUUNUVMPMZPMZV
        UCUUIUYRUUOUYSVUFPUUIUYRKKUOZKUUNUOZPMZUUOUUIUVCUWKUYRVUJNZUVEUWNKXPOZU
        XLUWKVUKUVFVULUTKXQVTUXMKKUUNXRWNVJUUIVUHKVUIUUNPVUHKNUUIKUXMXSXCUUIUVC
        UWKVUIUUNNUVEUWNUUNXTVJYAWRUUIUYSUUNKUOZUVMPMZVUFUUIUUNXPOZUWKUYSVUNNZU
        UIUVCVUOUVEUUNXQVCUWOVUOUXLUWKVUPUXMUUNKUUNXRYCSUUIVUMUUNUVMPUUIUVCUWKV
        UMUUNNUVEUWNUUNYDVJXGWRYAUUIUXLUWKVMUWKUWPVUGVUCNUUIUWKUXLUWOUXMYEUWOUW
        RKUUNUUNUVMXKYFWRUUIVUAUVNPMZVUCUVPUUIUVNVUBVUAPUUIUVCUWKUVNVUBNUVEUWNU
        UNXAVJWQUUIUWPUYDVUQUVPNZUWRUYFUXLUWPUYDVURUXMKUVMUVNXBWISWPYLXGUUIUUJU
        WBQUNUUIUUFUWAKPABCDEUUDGHIYGWQWQYHYIUUIUUFUPOZUUJUPOZUUKUUMVKZUUIUUEUR
        OZVUSUUDYJZURUPUUEEUVDUSZVCUVFVUSVUTUTKUUFVAVBQYKOVUTYMQYNUGVVAYOYPQUUJ
        YQXEVJYRUUIVVBVUSUUGUUKVKZVVCVVDVUSUVFVVEUTUUFKVHYSVJYRYT $.
        $( [28-Jul-2011] $) $( [23-Aug-2006] $)
    $}
  $}

$(
  @{
    opsqrlem8.1 @e |- T e. HrmOp @.
    opsqrlem8.2 @e |- 0hop <_op T @.
    opsqrlem8.3 @e |- T <_op Iop @.
    @( Lemma for ~ opsqri . @)
    opsqrlem8 @p |- E. u e. HrmOp ( 0hop <_op u /\
                    ( u o. u ) = T ) @=
      (  ) ? @.
      @( [ ?] @) @( [17-Aug-2006] @)
  @}

  @{
    @d u T @.
    @( Lemma for ~ opsqri . @)
    opsqrlem9 @p |- ( ( T e. HrmOp /\ 0hop <_op T /\ T <_op Iop ) ->
                 E. u e. HrmOp ( 0hop <_op u /\ ( u o. u ) = T ) ) @=
      ( cho wcel ch0o cleo wbr chio w3a cv ccom wceq wa wrex cif eqeq2 anbi2d
      rexbidv eleq1 breq2 breq1 3anbi123d 0hmop 0leop idleop 3pm3.2i elimhyp
      simp1i simp2i simp3i opsqrlem8 dedth ) BCDZEBFGZBHFGZIZEAJZFGZUQUQKZBL
      ZMZACNURUSUPBEOZLZMZACNBEBVBLZVAVDACVEUTVCURBVBUSPQRAVBVBCDZEVBFGZVBHFGZU
      PVFVGVHIECDZEEFGZEHFGZIBEVEUMVFUNVGUOVHBVBCSBVBEFTBVBHFUAUBEVBLVIVFVJVGVK
      VHEVBCSEVBEFTEVBHFUAUBVIVJVKUCUDUEUFUGZUHVFVGVHVLUIVFVGVHVLUJUKUL @.
      @( [ ?] @) @( [8-Aug-2006] @)
  @}

  @{
    @d u v T @.
    opsqr.1 @e |- T e. HrmOp @.
    opsqr.2 @e |- ( normop ` T ) e. RR @.
    opsqr.3 @e |- 0hop <_op T @.
    @( Square root lemma. @)
    opsqri @p |- E. u e. HrmOp ( 0hop <_op u /\
                    ( u o. u ) = T ) @=
      ( vv ch0o wceq cv cleo wbr ccom wa cho wrex eqid eqeq2 anbi2d rexbidv
      wne c1 cnop cfv cdiv co chot wcel chio opsqrlem9 cr cc0 clo hmoplin
      ax-mp nmlnop0iHIL biimpi necon3i rerecclzi syl hmopm mpan2 cle leopmuli
  mpanr2 mpanl2 clt wi 0re ltle mpan nmlnopgt0i recgt0i sylbi sylcOLD sylancOLD
    nmopleid mp3an12 syl3ancOLD opsqrlem1 adantl 0hmop 0leop ho0f ho0coi pm3.2i
      breq2 coeq1 coeq2 eqtrd eqeq1d anbi12d rcla4ev mp2an a1i pm2.61ne ) GGHZG
      AIZJKZWQWQLZBHZMZANOZGPWPXBWRWSGHZMZANOZBGBGHZXAXDANXFWTXCWRBGWSQRSBGTZXB
      WPAFUABUBUCZUDUEZBUFUEZBCDEXJPXJNUGZGXJJKZXJUHJKZGFIZJKXNXNLXJHMFNOXGFXJU
      IXGXIUJUGZXKXGXHUKTXOXHUKBGXHUKHXFBBNUGZBULUGCBUMUNZUOUPUQXHDURUSZXOXPXKC
      XIBUTVAUSXOUKXIVBKZXLXGXOXPXSXLCXOXPMXSGBJKXLEXIBVCVDVEXRXOUKXIVFKZXSXGUK
      UJUGXOXTXSVGVHUKXIVIVJXRXGUKXHVFKXTBXQVKXHDVLVMVNVOXPXHUJUGXGXMCDBVPVQVRV
      SVTXEWPGNUGGGJKZGGLZGHZMZXEWAYAYCWBGWCWDWEXDYDAGNWQGHZWRYAXCYCWQGGJWFYEWS
      YBGYEWSGWQLYBWQGWQWGWQGGWHWIWJWKWLWMWNWOUN @.
      @( [ ?] @) @( [8-Aug-2006] @)
  @}
$)

