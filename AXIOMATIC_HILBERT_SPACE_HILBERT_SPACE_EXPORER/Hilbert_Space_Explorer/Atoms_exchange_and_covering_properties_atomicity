$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Atoms, exchange and covering properties, atomicity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.
    $( The Hilbert lattice has the covering property.  Proposition 1(ii) of
       [Kalmbach] p. 140 (and its converse). $)
    chcv1 $p |- ( ( A e. CH /\ B e. HAtoms ) ->
                ( -. B C_ A <-> A <oH ( A vH B ) ) ) $=
      ( vx cch wcel cat wa wss wn chj co ccv wbr cv c0v wne csn cspn chil wi
      wceq wrex atom1d spansncv2 sseq1 notbid breq2d imbi12d syl5ibrcom adantld
      cfv oveq2 rexlimdva sylan2b atelch wpss chjcl cvpss syldan sylibrd sylan2
      imp chnle impbid ) ADEZBFEZGBAHZIZAABJKZLMZVFVECNZOPZBVKQRUKZUAZGZCSUBZVH
      VJTZCBUCVEVPVQVEVOVQCSVEVKSEGZVNVQVLVRVQVNVMAHZIZAAVMJKZLMZTAVKUDVNVHVTVJ
      WBVNVGVSBVMAUEUFVNVIWAALBVMAJULUGUHUIUJUMVBUNVFVEBDEZVJVHTBUOVEWCGVJAVIUP
      ZVHVEWCVIDEVJWDTABUQAVIURUSABVCUTVAVD $.
      $( [11-Jun-2004] $)

    $( The Hilbert lattice has the covering property. $)
    chcv2 $p |- ( ( A e. CH /\ B e. HAtoms ) ->
                ( A C. ( A vH B ) <-> A <oH ( A vH B ) ) ) $=
      ( cch wcel cat wa wss wn chj co wpss ccv wbr wb atelch chnle sylan2 chcv1
      bitr3d ) ACDZBEDZFBAGHZAABIJZKZAUCLMUATBCDUBUDNBOABPQABRS $.
      $( [11-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The join of a closed subspace and an atom equals their subspace sum.
       Special case of remark in [Kalmbach] p. 65, stating that if ` A ` or
       ` B ` is finite dimensional, then this equality holds. $)
    chjatom $p |- ( ( A e. CH /\ B e. HAtoms ) -> ( A +H B ) = ( A vH B ) ) $=
      ( vx cch wcel cat cph co chj wceq cv c0v wne csn cspn cfv wa chil oveq2
      wi wrex atom1d spansnj eqeq12d syl5ibr exp3a adantl com3l rexlimdv syl5bi
      imp ) ADEZBFEZABGHZABIHZJZUMCKZLMZBUQNOPZJZQZCRUAULUPCBUBULVAUPCRVAULUQRE
      ZUPUTULVBUPTTURUTULVBUPULVBQUPUTAUSGHZAUSIHZJAUQUCUTUNVCUOVDBUSAGSBUSAISU
      DUEUFUGUHUIUJUK $.
      $( [27-Feb-2005] $) $( [4-Jun-2004] $)
  $}

  ${
    $d x y A $.
    shatomic.1 $e |- A e. SH $.
    $( The lattice of Hilbert subspaces is atomic, i.e. any nonzero element is
       greater than or equal to some atom.  Part of proof of Theorem 16.9 of
       [MaedaMaeda] p. 70. $)
    shatomici $p |- ( A =/= 0H -> E. x e. HAtoms x C_ A ) $=
      ( vy c0h wne cv c0v wrex wss cat shne0i wcel csn cort cfv chil sheli h1da
      wa sylan csh sh1dle mpan adantr sseq1 rcla4ev syl2anc rexlimiva sylbi ) B
      EFDGZHFZDBIAGZBJZAKIZDBCLULUODBUKBMZULTUKNOPOPZKMZUQBJZUOUPUKQMULURUKBCRU
      KSUAUPUSULBUBMUPUSCBUKUCUDUEUNUSAUQKUMUQBUFUGUHUIUJ $.
      $( [30-Jun-2006] $) $( [24-Nov-2004] $)
  $}

  ${
    $d x y A $.
    hatomic.1 $e |- A e. CH $.
    $( The Hilbert lattice is atomic, i.e. any nonzero element is greater than
       or equal to some atom.  Remark in [Kalmbach] p. 140. $)
    hatomici $p |- ( A =/= 0H -> E. x e. HAtoms x C_ A ) $=
      ( chshii shatomici ) ABBCDE $.
      $( [2-Jul-2006] $) $( [22-Jul-2001] $)
  $}

  ${
    $d x y A $.
    $( A Hilbert lattice is atomic, i.e. any nonzero element is greater than or
       equal to some atom.  Remark in [Kalmbach] p. 140.  Also Definition 3.4-2
       in [MegPav2000] p. 2345 (PDF p. 8). $)
    hatomic $p |- ( ( A e. CH /\ A =/= 0H ) -> E. x e. HAtoms x C_ A ) $=
      ( cch wcel c0h wne cv wss cat wrex wi cif wceq neeq1 sseq2 rexbidv h0elch
      imbi12d elimel hatomici dedth imp ) BCDZBEFZAGZBHZAIJZUCUDUGKUCBELZEFZUEU
      HHZAIJZKBEBUHMZUDUIUGUKBUHENULUFUJAIBUHUEOPRAUHBECQSTUAUB $.
      $( [7-Oct-2006] $) $( [24-Jun-2004] $)
  $}

  ${
    $d x y A $.
    shatomistic.1 $e |- A e. SH $.
    $( The lattice of Hilbert subspaces is atomistic, i.e. any element is the
       supremum of its atoms.  Part of proof of Theorem 16.9 of [MaedaMaeda]
       p. 70. $)
    shatomistici $p |- A = ( span ` U. { x e. HAtoms | x C_ A } ) $=
      ( vy wss cat crab cuni cspn cfv wcel c0v wceq chil csh spanid 3syl adantr
      cv mpan eleq1 wne wa sheli spansnsh spansna sylan spansnss sseq1 sylanbrc
      csn elrab elssuni cch atssch chsssh sstri rabss2 uniss mp2b unimax shssii
      ax-mp eqsstri spanss eqsstr3d spansnid syl sseldd spancl sh0 a1i pm2.61ne
      ssriv mp2an fveq2i eqtri sseqtri eqssi ) BASZBEZAFGZHZIJZDBWDDSZBKZWEWDKL
      WDKZWELWELWDUAWFWELUBZUCZWEUKIJZWDWEWIWJWJIJZWDWFWKWJMZWHWFWENKZWJOKWLWEB
      CUDZWEUEWJPQRWIWJWBKZWJWCEZWKWDEZWIWJFKZWJBEZWOWFWMWHWRWNWEUFUGWFWSWHBOKZ
      WFWSCBWEUHTRWAWSAWJFVTWJBUIULUJWJWBUMWCNEZWPWQWCWAAOGZHZNFOEWBXBEWCXCEZFU
      NOUOUPUQWAAFOURWBXBUSUTZXCBNWTXCBMCABOVAVCZBCVBVDZUQZWJWCVETQVFWFWEWJKZWH
      WFWMXIWNWEVGVHRVIWGWFXAWDOKWGXHWCVJWDVKUTVLVMVNWDXCIJZBXCNEXDWDXJEXGXEWCX
      CVEVOXJBIJZBXCBIXFVPWTXKBMCBPVCVQVRVS $.
      $( [26-Nov-2004] $)
  $}

  ${
    $d x y A $.
    hatomistic.1 $e |- A e. CH $.
    $( ` CH ` is atomistic, i.e. any element is the supremum of its atoms.
       Remark in [Kalmbach] p. 140. $)
    hatomistici $p |- A = ( \/H ` { x e. HAtoms | x C_ A } ) $=
      ( vy cv wss cat crab chsup cfv cch wcel ssrab2 ax-mp wa elrab wi wceq c0h
      wn atssch sstri chsupcl chshii atelch sseq1 3imtr4i ssriv chsupss chsupid
      anim1i mp2an sseqtri cort cin wrex elssuni sylbir chsupunss syl6ss ex wne
      cuni atne0 adantr ssin chocini sseq2i bitr2i wb chle0 syl5bbr biimpa expr
      syl necon3ad mpd syld imnan sylnib nrex choccli chincli hatomici necon1bi
      sylib omlsii eqcomi ) AEZBFZAGHZIJZBWLBWKKFZWLKLWKGKWJAGMUAUBZWKUCNZBCUDW
      LWJAKHZIJZBWKWPFZWLWQFZDWKWPDEZGLZWTBFZOZWTKLZXBOWTWKLZWTWPLXAXDXBWTUEZUK
      WJXBAWTGWIWTBUFZPZWJXBAWTKXGPUGUHWMWPKFWRWSQWNWJAKMWKWPUIULNBKLWQBRCABUJN
      UMWTBWLUNJZUOZFZDGUPZTXJSRXKDGXAXBWTXIFZOZXKXAXBXMTZQXNTXAXBWTWLFZXOXAXBX
      PXCWTWKVCZWLXCXEWTXQFXHWTWKUQURWMXQWLFWNWKUSNUTVAXAXPXOXAXPOZWTSVBZXOXAXS
      XPWTVDVEXRXMWTSXAXPXMWTSRZXAXPXMOZXTYAWTSFZXAXTYAWTWLXIUOZFYBWTWLXIVFYCSW
      TWLWOVGVHVIXAXDYBXTVJXFWTVKVOVLVMVNVPVQVAVRXBXMVSWFWTBXIVFVTWAXLXJSDXJBXI
      CWLWOWBWCWDWENWGWH $.
      $( [14-Aug-2002] $)
  $}

  ${
    $d x A $.  $d x B $.
    chpssat.1 $e |- A e. CH $.
    chpssat.2 $e |- B e. CH $.
    $( Two Hilbert lattice elements in a proper subset relationship imply the
       existence of an atom less than or equal to one but not the other. $)
    chpssati $p |- ( A C. B -> E. x e. HAtoms ( x C_ B /\ -. x C_ A ) ) $=
      ( wpss wss wn cat wral crab chsup cfv cch ssrab2 atssch sstri hatomistici
      wi sylbir cv wrex dfpss3 simprbi iman ralbii ss2rab chsupss mp2an 3sstr4g
      wa con3i dfrex2 sylibr syl ) BCFZCBGZHZAUAZCGZUSBGZHUKZAIUBZUPBCGURBCUCUD
      URVBHZAIJZHVCVEUQVEUTVASZAIJZUQVFVDAIUTVAUEUFVGUTAIKZVAAIKZGZUQUTVAAIUGVJ
      VHLMZVILMZCBVHNGVINGVJVKVLGSVHINUTAIOPQVIINVAAIOPQVHVIUHUIACERABDRUJTTULV
      BAIUMUNUO $.
      $( [10-Jun-2004] $)

    $( The Hilbert lattice is relatively atomic.  Remark 2 of [Kalmbach]
       p. 149. $)
    chrelati $p |- ( A C. B ->
                  E. x e. HAtoms ( A C. ( A vH x ) /\ ( A vH x ) C_ B ) ) $=
      ( wpss cv wss wn wa cat wrex chj co chpssati wcel ancom cch wb pssss mpan
      atelch chnle adantl chlub mp3an13 sylan9bb anbi12d syl2an syl5bb rexbidva
      ibar mpbid ) BCFZAGZCHZUOBHIZJZAKLBBUOMNZFZUSCHZJZAKLABCDEOUNURVBAKURUQUP
      JZUNUOKPZJVBUPUQQUNBCHZUORPZVCVBSVDBCTUOUBVEVFJUQUTUPVAVFUQUTSZVEBRPZVFVG
      DBUOUCUAUDVEUPVEUPJZVFVAVEUPULVHVFCRPVIVASDEBUOCUEUFUGUHUIUJUKUM $.
      $( [11-Jun-2004] $)

    $( A consequence of relative atomicity. $)
    chrelat2i $p |- ( -. A C_ B <-> E. x e. HAtoms ( x C_ A /\ -. x C_ B ) ) $=
      ( wss wn wa cat wrex wpss wcel cch wi wb syl6bir wo sylib con2i syl cv co
      cin nssinpss chj chincli chrelati atelch chlub mp3an13 simpr adantld ssin
      notbii chnle mpan syl5bb anbi12d pm3.21 ianor notnot orbi1i imor 3bitr2ri
      orcom bitr4i adantrl jcad reximia sylbi wral sstr2 com12 ralrimivw ralbii
      iman ralnex bitri impbii ) BCFZGZAUAZBFZWBCFZGZHZAIJZWABCUCZBKZWGBCUDWIWH
      WHWBUEUBZKZWJBFZHZAIJWGAWHBBCDEUFZDUGWMWFAIWBILWBMLZWMWFNWBUHWOWMWCWEWOWL
      WCWKWOWLWHBFZWCHZWCWHMLZWOBMLWQWLOWNDWHWBBUIUJZWPWCUKPULWOWMWCWDHZGZWQHWE
      WOXAWKWQWLXAWBWHFZGZWOWKWTXBWBBCUMUNWRWOXCWKOWNWHWBUOUPUQWSURXAWCWEWPWDXA
      WCHZWDWCWTNZXDGZWDWCUSXFXAGZWCGZQWTXHQZXEXAWCUTWTXGXHWTVAVBXIXHWTQXEWTXHV
      EWCWTVCVFVDRSVGPVHTVITVJVTWGVTWCWDNZAIVKZWGGZVTXJAIWCVTWDWBBCVLVMVNXKWFGZ
      AIVKXLXJXMAIWCWDVPVOWFAIVQVRRSVS $.
      $( [30-Jun-2004] $)

    $( If a Hilbert lattice element covers another, it equals the other joined
       with some atom.  This is a consequence of the relative atomicity of
       Hilbert space. $)
    cvati $p |- ( A <oH B -> E. x e. HAtoms ( A vH x ) = B ) $=
      ( ccv wbr cv chj co wpss wss wa cat wrex wceq cch wcel wi syl cvpss mp2an
      chrelati atelch chjcl sylancr cvnbtwn2 mp3an12 com12 reximdvai mpd ) BCFG
      ZBBAHZIJZKUNCLMZANOZUNCPZANOULBCKZUPBQRZCQRZULURSDEBCUAUBABCDEUCTULUOUQAN
      UMNRZULUOUQSZVAUNQRZULVBSZVAUSUMQRVCDUMUDBUMUEUFUSUTVCVDDEBCUNUGUHTUIUJUK
      $.
      $( [30-Nov-2004] $)

    $( An alternate way to express the covering property. $)
    cvbr3i $p |- ( A <oH B <-> ( A C. B /\ E. x e. HAtoms ( A vH x ) = B ) ) $=
      ( ccv wbr wpss cv chj co wceq cat wrex wa cch wcel wi wb adantl cvpss jca
      mp2an cvati chcv2 mpan adantr psseq2 breq2 3bitr3d biimpd ex rexlimdv imp
      com3r impbii ) BCFGZBCHZBAIZJKZCLZAMNZOUQURVBBPQZCPQUQURRDEBCUAUCABCDEUDU
      BURVBUQURVAUQAMUSMQZVAURUQVDVAURUQRVDVAOZURUQVEBUTHZBUTFGZURUQVDVFVGSZVAV
      CVDVHDBUSUEUFUGVAVFURSVDUTCBUHTVAVGUQSVDUTCBFUITUJUKULUOUMUNUP $.
      $( [30-Nov-2004] $)

    $( Lemma for ~ cvexchi . $)
    cvexchlem $p |- ( ( A i^i B ) <oH B -> A <oH ( A vH B ) ) $=
      ( vx wss wn wa cat ccv wbr chj co wpss cch wcel wi wb mpan wceq cin cvpss
      cv wrex chincli mp2an chpssati syl ssin ancom bitr3i baibr notbid biimpar
      chcv1 biimpa sylan2 adantrr atelch chabs1i oveq1i chjass mp3an12 syl5reqr
      adantr chnle inss2 biantrur chlub mp3an13 syl5bb chjcl com23 sylbid imp32
      anbi12d cvnbtwn2 oveq2d eqtr3d sylan breqtrd exp32 rexlimiv mpcom ) EUCZB
      FZWEABUAZFZGZHZEIUDZWGBJKZAABLMZJKZWLWGBNZWKWGOPZBOPZWLWOQABCDUEZDWGBUBUF
      EWGBWRDUGUHWJWLWNQEIWEIPZWJWLWNWSWJWLHZHAAWELMZWMJWSWJAXAJKZWLWJWSWEAFZGZ
      XBWFXDWIWFXCWHWHWFXCWHXCWFHWFXCHWEABUIXCWFUJUKULUMUNWSXDXBAOPZWSXDXBRCAWE
      UOSUPUQURWSWEOPZWTXAWMTWEUSXFWTHZAWGWELMZLMZXAWMXFXIXATWTXFXAAWGLMZWELMZX
      IXJAWELABCDUTVAXEWPXFXKXITCWRAWGWEVBVCVDVEXGXHBALXFWJWLXHBTZXFWJWGXHNZXHB
      FZHZWLXLQWJWIWFHXFXOWFWIUJXFWIXMWFXNWPXFWIXMRWRWGWEVFSWFWGBFZWFHZXFXNXPWF
      ABVGVHWPXFWQXQXNRWRDWGWEBVIVJVKVPVKXFWLXOXLXFXHOPZWLXOXLQQZWPXFXRWRWGWEVL
      SWPWQXRXSWRDWGBXHVQVCUHVMVNVOVRVSVTWAWBWCWD $.
      $( [10-Jun-2004] $)

    $( The Hilbert lattice satisfies the exchange axiom.  Proposition 1(iii) of
       [Kalmbach] p. 140 and its converse.  Originally proved by Garrett
       Birkhoff in 1933. $)
    cvexchi $p |- ( ( A i^i B ) <oH B <-> A <oH ( A vH B ) ) $=
      ( cin ccv wbr chj co cvexchlem cort cfv choccli eqtri 3imtr4i cch wcel wb
      cvcon3 mp2an chdmj1i breq1i chdmm1i chjcomi breq2i chjcli chincli impbii
      incom ) ABEZBFGZAABHIZFGZABCDJULKLZAKLZFGZBKLZUJKLZFGZUMUKUQUOEZUOFGUQUQU
      OHIZFGUPUSUQUOBDMZACMZJUNUTUOFUNUOUQEUTABCDUAUOUQUINUBURVAUQFURUOUQHIVAAB
      CDUCUOUQVCVBUDNUEOAPQULPQUMUPRCABCDUFAULSTUJPQBPQUKUSRABCDUGDUJBSTOUH $.
      $( [12-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A consequence of relative atomicity. $)
    chrelat2 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( -. A C_ B <-> E. x e. HAtoms ( x C_ A /\ -. x C_ B ) ) ) $=
      ( cch wcel wss wn wa cat wrex wb chil cif wceq notbid sseq2 rexbidv helch
      bibi12d elimel cv sseq1 anbi1d anbi2d chrelat2i dedth2h ) BDEZCDEZBCFZGZA
      UAZBFZUKCFZGZHZAIJZKUGBLMZCFZGZUKUQFZUNHZAIJZKUQUHCLMZFZGZUTUKVCFZGZHZAIJ
      ZKBCLLBUQNZUJUSUPVBVJUIURBUQCUBOVJUOVAAIVJULUTUNBUQUKPUCQSCVCNZUSVEVBVIVK
      URVDCVCUQPOVKVAVHAIVKUNVGUTVKUMVFCVCUKPOUDQSAUQVCBLDRTCLDRTUEUF $.
      $( [1-Jul-2004] $)

    $( A consequence of relative atomicity. $)
    chrelat3 $p |- ( ( A e. CH /\ B e. CH ) ->
                   ( A C_ B <-> A. x e. HAtoms ( x C_ A -> x C_ B ) ) ) $=
      ( cch wcel wa wss cv wn cat wral wrex chrelat2 dfrex2 syl6bb con4bid iman
      wi ralbii syl6bbr ) BDECDEFZBCGZAHZBGZUCCGZIFZIZAJKZUDUERZAJKUAUBUHUAUBIU
      FAJLUHIABCMUFAJNOPUIUGAJUDUEQST $.
      $( [2-Jul-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    chrelat3.1 $e |- A e. CH $.
    chrelat3.2 $e |- B e. CH $.
    $( A consequence of the relative atomicity of Hilbert space: the ordering
       of Hilbert lattice elements is completely determined by the atoms they
       majorize. $)
    chrelat3i $p |- ( A C_ B <-> A. x e. HAtoms ( x C_ A -> x C_ B ) ) $=
      ( cch wcel wss cv wi cat wral wb chrelat3 mp2an ) BFGCFGBCHAIZBHPCHJAKLMD
      EABCNO $.
      $( [29-May-2005] $) $( [30-Jun-2004] $)

    $( A consequence of relative atomicity.  Extensionality principle: two
       lattice elements are equal iff they majorize the same atoms. $)
    chrelat4i $p |- ( A = B <-> A. x e. HAtoms ( x C_ A <-> x C_ B ) ) $=
      ( wss wa cv wi cat wral wceq wb chrelat3i anbi12i eqss ralbiim 3bitr4i )
      BCFZCBFZGAHZBFZUACFZIAJKZUCUBIAJKZGBCLUBUCMAJKSUDTUEABCDENACBEDNOBCPUBUCA
      JQR $.
      $( [26-Jul-2005] $) $( [30-Jun-2004] $)
  $}

  $( The Hilbert lattice satisfies the exchange axiom.  Proposition 1(iii) of
     [Kalmbach] p. 140 and its converse.  Originally proved by Garrett Birkhoff
     in 1933. $)
  cvexch $p |- ( ( A e. CH /\ B e. CH ) ->
                ( ( A i^i B ) <oH B <-> A <oH ( A vH B ) ) ) $=
    ( cch wcel cin ccv wbr chj co wb chil cif wceq ineq1 breq1d breq12d bibi12d
    id helch elimel oveq1 ineq2 oveq2 breq2d cvexchi dedth2h ) ACDZBCDZABEZBFGZ
    AABHIZFGZJUGAKLZBEZBFGZUMUMBHIZFGZJUMUHBKLZEZURFGZUMUMURHIZFGZJABKKAUMMZUJU
    OULUQVCUIUNBFAUMBNOVCAUMUKUPFVCRAUMBHUAPQBURMZUOUTUQVBVDUNUSBURFBURUMUBVDRP
    VDUPVAUMFBURUMHUCUDQUMURAKCSTBKCSTUEUF $.
    $( [21-Jun-2004] $)

  $( The Hilbert lattice satisfies the covering property of Definition 7.4 of
     [MaedaMaeda] p. 31 and its converse. $)
  cvp $p |- ( ( A e. CH /\ B e. HAtoms ) ->
            ( ( A i^i B ) = 0H <-> A <oH ( A vH B ) ) ) $=
    ( cch wcel cat wa cin ccv wbr c0h wceq chj co atelch chincl sylan2 sylancom
    wb atcveq0 cvexch bitr3d ) ACDZBEDZFABGZBHIZUDJKZAABLMHIZUBUCUDCDZUEUFRUCUB
    BCDZUHBNZABOPUDBSQUCUBUIUEUGRUJABTPUA $.
    $( [21-Jun-2004] $)

  $( The meet of a Hilbert lattice element and an incomparable atom is the zero
     subspace. $)
  atnssm0 $p |- ( ( A e. CH /\ B e. HAtoms ) ->
             ( -. B C_ A <-> ( A i^i B ) = 0H ) ) $=
    ( cch wcel cat wa wss wn chj co ccv wbr cin c0h wceq chcv1 cvp bitr4d ) ACD
    BEDFBAGHAABIJKLABMNOABPABQR $.
    $( [30-Jun-2004] $)

  $( The meet of distinct atoms is the zero subspace. $)
  atnemeq0 $p |- ( ( A e. HAtoms /\ B e. HAtoms ) ->
             ( A =/= B <-> ( A i^i B ) = 0H ) ) $=
    ( cat wcel wa wss wn wne cin c0h wceq atsseq eqcom syl6bb ancoms necon3bbid
    wb cch atelch atnssm0 sylan bitr3d ) ACDZBCDZEZBAFZGZABHABIJKZUEUFABUDUCUFA
    BKZQUDUCEUFBAKUIBALBAMNOPUCARDUDUGUHQASABTUAUB $.
    $( [25-Sep-2009] $) $( [25-Jun-2004] $)

  $( The meet with an atom's superset is the atom. $)
  atssma $p |- ( ( A e. HAtoms /\ B e. CH ) ->
            ( A C_ B <-> ( A i^i B ) e. HAtoms ) ) $=
    ( cat wcel cch wa wss cin wi wceq df-ss biimpi eleq1d biimprcd adantr incom
    c0h wn eleq1i wne atne0 df-ne sylib sylbi atnssm0 ancoms biimpd syl5 impbid
    wb con1d ) ACDZBEDZFZABGZABHZCDZULUOUQIUMUOUQULUOUPACUOUPAJABKLMNOUQBAHZQJZ
    RZUNUOUQURCDZUTUPURCABPSVAURQTUTURUAURQUBUCUDUNUOUSUNUORZUSUMULVBUSUJBAUEUF
    UGUKUHUI $.
    $( [12-Jun-2006] $)

  $( Two atoms covering the zero subspace are equal. $)
  atcv0eq $p |- ( ( A e. HAtoms /\ B e. HAtoms ) ->
               ( 0H <oH ( A vH B ) <-> A = B ) ) $=
    ( cat wcel wa c0h chj co ccv wbr wne wn cin atnemeq0 cch wb atelch atcv0 wi
    wceq sylan adantr biantrurd 3bitrd chjcl h0elch mp3an1 syldan syl2an sylbid
    cvp cvntr necon4ad oveq1 chjidm syl sylan9eq eqcomd eleq1d ex adantl impbid
    ibd syl6com ) ACDZBCDZEZFABGHZIJZABTZVGVIABVGABKZFAIJZAVHIJZEZVILZVGVKABMFT
    ZVMVNABNVEAODZVFVPVMPAQZABUKUAVGVLVMVEVLVFARUBUCUDVEVQBODZVNVOSZVFVRBQZVQVS
    VHODZVTABUEFODVQWBVTUFFAVHULUGUHUIUJUMVFVJVISVEVJVFVHCDZVIVJVFWCVJVFVFWCPVJ
    VFEZBVHCWDVHBVJVFVHBBGHZBABBGUNVFVSWEBTWABUOUPUQURUSUTVCVHRVDVAVB $.
    $( [26-Jun-2004] $)

  $( Two atoms covering the zero subspace are equal. $)
  atcv1 $p |- ( ( ( A e. CH /\ B e. HAtoms /\ C e. HAtoms ) /\
                A <oH ( B vH C ) ) -> ( A = 0H <-> B = C ) ) $=
    ( cch wcel cat w3a chj co ccv wbr wa c0h wceq wi breq1 biimpd ex imp wb syl
    atcv0eq sylan9bbr com23 3adant1 atelch chjidm sylan9eq eqcomd eleq1d impcom
    oveq1 ibd atcveq0 sylan2 exp32 com34 3adant2 impbid ) ADEZBFEZCFEZGZABCHIZJ
    KZLAMNZBCNZVCVEVFVGOZVAVBVEVHOUTVAVBLZVFVEVGVIVFVEVGOVIVFLVEVGVFVEMVDJKVIVG
    AMVDJPBCUBUCQRUDUESVCVEVGVFOZUTVBVEVJOZVAUTVBVKUTVBVGVEVFUTVBVGVEVFOUTVBVGL
    ZLVEVFVLUTVDFEZVEVFTVGVBVMVGVBVMVGVBVBVMTVGVBLZCVDFVNVDCVGVBVDCCHIZCBCCHULV
    BCDEVOCNCUFCUGUAUHUIUJRUMUKAVDUNUOQUPUQSURSUS $.
    $( [26-Jun-2004] $)

  $( The Hilbert lattice satisfies the atom exchange property.  Proposition
     1(i) of [Kalmbach] p. 140.  A version of this theorem related to vector
     analysis was originally proved by Hermann Grassmann in 1862.  Also
     Definition 3.4-3(b) in [MegPav2000] p. 2345 (PDF p. 8) (use ~ atnemeq0 to
     obtain atom inequality). $)
  atexch $p |- ( ( A e. CH /\ B e. HAtoms /\ C e. HAtoms ) ->
            ( ( B C_ ( A vH C ) /\ ( A i^i B ) = 0H ) -> C C_ ( A vH B ) ) ) $=
    ( cch wcel cat w3a chj co wss wceq wa atelch sylan2 3adant2 wpss wi ccv wbr
    chjcl cin c0h chub2 ancoms adantr cvpss syldan sylbid 3adant3 adantld chub1
    cvp id a1d ancrd wb chlub syld3an3 sylibd syl3an adantrd jcad simp1 anim12d
    imp 3jca ancomsd psssstr syl6 chcv2 cvnbtwn2 sylsyld mpd sseqtr4d ex ) ADEZ
    BFEZCFEZGZBACHIZJZABUAUBKZLZCABHIZJVSWCLZCVTWDVSCVTJZWCVPVRWFVQVRVPCDEZWFCM
    ZWGVPWFCAUCUDNOUEWEAWDPZWDVTJZLZWDVTKZVSWCWKVSWCWIWJVSWBWIWAVPVQWBWIQVRVPVQ
    LWBAWDRSZWIABULVPVQWDDEZWMWIQVQVPBDEZWNBMZABTZNAWDUFUGUHUIZUJVSWAWJWBVPVPVQ
    WOVRWGWAWJQVPUMZWPWHVPWOWGGZWAAVTJZWALZWJWTWAXAWTXAWAVPWGXAWOACUKOUNUOVPWOW
    GVTDEZXBWJUPVPWGXCWOACTOZABVTUQURUSUTZVAVBVEVSWCWKWLQZVSVPXCWNGZWCAVTRSZXFV
    PVPVQWOVRWGXGWSWPWHWTVPXCWNVPWOWGVCXDVPWOWNWGWQUIVFUTVSWCAVTPZXHVSWCWKXIVSW
    BWAWKVSWBWIWAWJWRXEVDVGAWDVTVHVIVPVRXIXHUPVQACVJOUSAVTWDVKVLVEVMVNVO $.
    $( [27-Jun-2004] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    atoml.1 $e |- A e. CH $.
    $( An assertion holding in atomic orthomodular lattices that is equivalent
       to the exchange axiom.  Proposition 3.2.17 of [PtakPulmannova] p. 66. $)
    atomli $p |- ( B e. HAtoms ->
                ( ( A vH B ) i^i ( _|_ ` A ) ) e. ( HAtoms u. { 0H } ) ) $=
      ( vx cat wcel chj co cort cin c0h wceq wa wss cch wi sylancr mpan2 ex imp
      cfv wo csn cun wne cv wrex atelch chjcl choccli chincl hatomic 3syl inss2
      wb sstr pjococi oveq1i ineq1i incom eqtr3i pjoml3 syl5eq syl2an ad2ant2lr
      mpan inss1 chub1 adantr chlub mp3an1 sylan2 biimpd ancoms adantrr anim12i
      mpand ad2antlr pm3.22 adantl chsh chshii orthin sylancl atexch sylc exp3a
      csh jca syl3c eqssd ineq1d eqtr3d eleq1d exp43 com24 imp31 com23 rexlimdv
      ibd mpd necon1bd orrd elun fvex inex2 elsnc orbi2i bitri sylibr ) BEFZABG
      HZAIUAZJZEFZXNKLZUBZXNEKUCZUDFZXKXOXPXKXOXNKXKXNKUEZXOXKXTMZDUFZXNNZDEUGZ
      XOXKXTYDXKXLOFZXNOFZXTYDPXKAOFZBOFZYECBUHZABUIZQYEXMOFZYFACUJZXLXMUKRYFXT
      YDDXNULSUMTYAYCXODEYAYCYBEFZXOYAYCYMXOPYAYCMYMXOXKXTYCYMYMXOUOZPXKYMYCXTY
      NXKYMYCXTYNXKYMMZYCXTMZMZYBXNEYQAYBGHZXMJZYBXNYMYCYSYBLZXKXTYMYBOFZYBXMNZ
      YTYCYBUHZYCXNXMNUUBXLXMUNYBXNXMUPRZUUAUUBMZYSXMXMIUAZYBGHZJZYBUUGXMJYSUUH
      UUGYRXMUUFAYBGACUQURUSUUGXMUTVAUUAUUBUUHYBLZYKUUAUUBUUIPYLXMYBVBVFTVCVDVE
      YQYRXLXMYQYRXLYOYCYRXLNZXTYCYOYBXLNZUUJYCXNXLNUUKXLXMVGYBXNXLUPRZYOUUKUUJ
      XKYHUUAUUKUUJPYMYIUUCYHUUAMAXLNZUUKUUJYHUUMUUAYGYHUUMCABVHVFVIUUAYHUUMUUK
      MZUUJPUUAYHMUUNUUJYHUUAYEUUNUUJUOZYGYHYECYJVFYGUUAYEUUOCAYBXLVJVKVLVMVNVQ
      VDTVLVOYQYHYROFZMZAYRNZBYRNZXLYRNZYOUUQYPXKYHYMUUPYIYMYGUUAUUPCUUCAYBUIQV
      PVIYMUURXKYPYMYGUUAUURCUUCAYBVHQVRYQYMXKMZUUKAYBJZKLZMZUUSYOUVAYPXKYMVSVI
      YMYCUVDXKXTYMYCMUUKUVCYCUUKYMUULVTYMUUAUUBUVCYCUUCUUDUUEUVBYBAJZKAYBUTUUA
      UUBUVEKLZUUAYBWHFAWHFUUBUVFPYBWAACWBYBAWCWDTVCVDWIVEYGYMXKUVDUUSPCAYBBWEV
      KWFUUQUURUUSUUTUUQUURUUSMZUUTYGYHUUPUVGUUTUOCABYRVJVKVMWGWJWKWLWMWNWOWPWQ
      WTSWRWSXASXBXCXSXOXNXRFZUBXQXNEXRXDUVHXPXOXNKXMXLAIXEXFXGXHXIXJ $.
      $( [9-May-2005] $) $( [24-Jun-2004] $)

    $( An assertion holding in atomic orthomodular lattices that is equivalent
       to the exchange axiom.  Proposition P8(ii) of [BeltramettiCassinelli1]
       p. 400. $)
    atoml2i $p |- ( ( B e. HAtoms /\ -. B C_ A ) ->
                ( ( A vH B ) i^i ( _|_ ` A ) ) e. HAtoms ) $=
      ( cat wcel wss wn chj co cort cfv cin c0h cch atelch pjoml5 sylancr incom
      wceq wo eqeq1i biimpi oveq2d chj0i syl6eq sylan9req ex wb chlejb2 sylancl
      sylibrd con3d csn cun atomli elun h0elch elexi elsnc2 orbi2i orcom 3bitri
      sylib ord syld imp ) BDEZBAFZGZABHIZAJKZLZDEZVGVIVLMSZGVMVGVNVHVGVNVJASZV
      HVGVNVOVGVNVJAVKVJLZHIZAVGANEZBNEZVQVJSCBOZABPQVNVQAMHIAVNVPMAHVNVPMSVLVP
      MVJVKRUAUBUCACUDUEUFUGVGVSVRVHVOUHVTCBAUIUJUKULVGVNVMVGVLDMUMZUNEZVNVMTZA
      BCUOWBVMVLWAEZTVMVNTWCVLDWAUPWDVNVMVLMMNUQURUSUTVMVNVAVBVCVDVEVF $.
      $( [12-Jun-2006] $)

    $( An ordering law for a Hilbert lattice atom and a commuting subspace. $)
    atordi $p |- ( ( B e. HAtoms /\ A C_H B ) ->
                ( B C_ A \/ B C_ ( _|_ ` A ) ) ) $=
      ( cat wcel ccm wbr wa wss cort cfv cin chj co cch c0h incom syl6eq adantr
      wceq wn atelch choccli chincl mpan chj0 syl h0elch chjcom sylancl chocini
      eqtr3d eqtri oveq1i syl6eqr w3a cmidi cmcm2ii mpanr1 mp3anl2 mpanl1 sylan
      fh2 eqtr4d atoml2i adantlr eqeltrd wb atssma mpan2 ad2antrr mpbird orrd
      ex ) BDEZABFGZHZBAIZBAJKZIZVQVRUAZVTVQWAHZVTBVSLZDEZWBWCABMNZVSLZDVQWCWFT
      WAVQWCVSWELZWFVOBOEZVPWCWGTBUBWHVPHWCVSALZVSBLZMNZWGWHWCWKTVPWHWCPWJMNZWK
      WHWJPMNZWCWLWHWMWJWCWHWJOEZWMWJTVSOEZWHWNACUCZVSBUDUEZWJUFUGVSBQRWHWNPOEW
      MWLTWQUHWJPUIUJULWIPWJMWIAVSLPVSAQACUKUMUNUOSWOWHVPWGWKTZWPWOAOEZWHVPWRCW
      OWSWHUPAVSFGVPWRAACCACUQURVSABVCUSUTVAVDVBVSWEQRSVOWAWFDEVPABCVEVFVGVOVTW
      DVHZVPWAVOWOWTWPBVSVIVJVKVLVNVM $.
      $( [12-Jun-2006] $)

    $( Lemma for ~ atcvati . $)
    atcvatlem $p |- ( ( ( B e. HAtoms /\ C e. HAtoms ) /\
        ( A =/= 0H /\ A C. ( B vH C ) ) ) -> ( -. B C_ A -> A e. HAtoms ) ) $=
      ( vx cat wcel wa c0h chj co wss wi ccv wceq imp cch wb atelch adantr wpss
      wne wn cv hatomici wbr cin sseq1 biimpcd necon3bd adantrl atnemeq0 syl5ib
      wrex cvp chjcom sylan2 breq2d bitrd sylan sylibd ancoms adantlr w3a simp1
      simp3 chjcl 3adant3 3jca syl3an chlub syl chub1 syl2an pssss adantl incom
      sstr syl5eq adantrr atexch syl3an1 mp2and mpbi2and 3adant2 syld3an3 mpbid
      anim12i syl3anl eqssd anassrs psseq2d exp32 3expa an32s com34 imp45 simpr
      ex ibd jca cvnbtwn3 mp3an3 exp4a com23 imp4a biimprcd exp4c pm2.43b exp4d
      eleq1d rexlimdva syl5 imp32 ) BFGZCFGZHZAIUBZABCJKZUAZBALZUCZAFGZMZXREUDZ
      ALZEFUNXQXTYDMZEADUEXQYFYGEFXQYEFGZHZYFXTYBYCXQYHYFXTYBHZHZYCMZXQYHYLYHXQ
      YHYKYCYIYKHZYCYHYMAYEFYMYEBYEJKZNUFZAYNUAZAYEOZYIYKYOXOYHYKYOMZXPYHXOYRYH
      XOHZYKYEBUGZIOZYOYKYEBUBZYSUUAYFYBUUBXTYFYBUUBYFYAYEBYEBOYFYAYEBAUHUIUJPZ
      UKYEBULZUMYHYEQGZXOUUAYORYESZUUEXOHZUUAYEYEBJKZNUFYOYEBUOUUGUUHYNYENXOUUE
      BQGZUUHYNOBSZYEBUPUQURUSUTVAVBVCPYIYFXTYBYPYIYFYBXTYPXOYHXPYFYBXTYPMZMMZX
      OYHXPUULXOYHXPVDZYFYBUUKUUMYFYBHZHZXTYPUUOXTXTYPRUUOXTHXSYNAUUMUUNXTXSYNO
      UUMUUNXTHZHZXSYNUUQBYNLZCYNLZXSYNLZUUMUURUUSHUUTRZUUPUUMUUICQGZYNQGZVDZUV
      AXOUUIYHUUEXPUVBUVDUUJUUFCSZUUIUUEUVBVDZUUIUVBUVCUUIUUEUVBVEUUIUUEUVBVFUU
      IUUEUVCUVBBYEVGZVHVIVJBCYNVKVLTUUMUURUUPXOYHUURXPXOUUIUUEUURYHUUJUUFBYEVM
      VNVHTUUQYEXSLZBYEUGZIOZUUSUUPUVHUUMYFXTUVHYBXTYFAXSLUVHAXSVOYEAXSVRUQVCZV
      PUUMUUNUVJXTUUOUVIYTIBYEVQUUMUUNUUAXOYHUUNUUAMZXPYHXOUVLUUNUUBYSUUAUUCUUD
      UMVBVHPVSVTUUMUVHUVJHUUSMZUUPXOUUIYHXPUVMUUJBYECWAWBTWCWDXOUUIYHUUEXPUVBU
      UPYNXSLZUUJUUFUVEUVFUUPHBXSLZUVHHZUVNUVFUVOUUPUVHUUIUVBUVOUUEBCVMWEUVKWHU
      VFUVPUVNRZUUPUUIUUEUVBXSQGZUVQUUIUVBUVRUUEBCVGWEBYEXSVKWFTWGWIWJWKWLWSWTW
      MWNWOWPWQYIYFYOYPHYQMZYJYIYFUVSXOYHYFUVSMZXPXOYHHUUEUVCHZUVTXOUUIUUEUWAYH
      UUJUUFUUIUUEHUUEUVCUUIUUEWRUVGXAVNUWAYFYOYPYQUWAYOYFYPYQMUWAYOYFYPYQUUEUV
      CAQGYOYFYPHYQMMDYEYNAXBXCXDXEXFVLVCPVTWCXKXGXHXIPXJXLXMXN $.
      $( [23-Mar-2007] $) $( [27-Jun-2004] $)

    $( A nonzero Hilbert lattice element less than the join of two atoms is an
       atom. $)
    atcvati $p |- ( ( B e. HAtoms /\ C e. HAtoms ) ->
                 ( ( A =/= 0H /\ A C. ( B vH C ) ) -> A e. HAtoms ) ) $=
      ( cat wcel wa c0h chj co wpss wss wn cch wi atelch syl2an imp atcvatlem
      ex wne wo w3a chlub 3comr ssnpss syl6bi con2d ianor syl6ib mp3an1 adantrl
      wb wceq chjcom psseq2d anbi2d sylbird ancoms jaod mpd ) BEFZCEFZGZAHUAZAB
      CIJZKZGZAEFZVDVHGZBALZMZCALZMZUBZVIVDVGVOVEVDVGVOVBBNFZCNFZVGVOOZVCBPZCPZ
      ANFZVPVQVRDWAVPVQUCZVGVKVMGZMVOWBWCVGWBWCVFALZVGMVPVQWAWCWDUMBCAUDUEVFAUF
      UGUHVKVMUIUJUKQRULVJVLVIVNABCDSVDVHVNVIOZVCVBVHWEOVCVBGZVHVEACBIJZKZGZWEW
      FWHVGVEWFWGVFAVCVQVPWGVFUNVBVTVSCBUOQUPUQWFWIWEACBDSTURUSRUTVAT $.
      $( [25-Mar-2007] $) $( [28-Jun-2004] $)

    $( A Hilbert lattice element covered by the join of two distinct atoms is
       an atom. $)
    atcvat2i $p |- ( ( B e. HAtoms /\ C e. HAtoms ) ->
                 ( ( -. B = C /\ A <oH ( B vH C ) ) -> A e. HAtoms ) ) $=
      ( cat wcel wa wceq wn chj co ccv wbr wi c0h wne cch wb atcv1 atelch chjcl
      mp3anl1 necon3abid wpss cvpss sylancr atcvati exp3acom23 syld imp sylbird
      syl2an ex com23 imp3a ) BEFZCEFZGZBCHZIZABCJKZLMZAEFZURVBUTVCURVBUTVCNURV
      BGZUTAOPZVCVDUSAOAQFZUPUQVBAOHUSRDABCSUBUCURVBVEVCNZURVBAVAUDZVGURVFVAQFZ
      VBVHNDUPBQFCQFVIUQBTCTBCUAULAVAUEUFURVEVHVCABCDUGUHUIUJUKUMUNUO $.
      $( [26-Jun-2004] $)
  $}

  $( An ordering law for a Hilbert lattice atom and a commuting subspace. $)
  atord $p |- ( ( A e. CH /\ B e. HAtoms /\ A C_H B ) ->
              ( B C_ A \/ B C_ ( _|_ ` A ) ) ) $=
    ( cch wcel cat ccm wbr wss cort cfv wo wa wi c0h cif wceq breq1 sseq2 fveq2
    anbi2d sseq2d orbi12d imbi12d h0elch elimel atordi dedth 3impib ) ACDZBEDZA
    BFGZBAHZBAIJZHZKZUIUJUKLZUOMUJUIANOZBFGZLZBUQHZBUQIJZHZKZMANAUQPZUPUSUOVCVD
    UKURUJAUQBFQTVDULUTUNVBAUQBRVDUMVABAUQISUAUBUCUQBANCUDUEUFUGUH $.
    $( [12-Jun-2006] $)

  $( A Hilbert lattice element covered by the join of two distinct atoms is an
     atom. $)
  atcvat2 $p |- ( ( A e. CH /\ B e. HAtoms /\ C e. HAtoms ) ->
               ( ( -. B = C /\ A <oH ( B vH C ) ) -> A e. HAtoms ) ) $=
    ( cch wcel cat wceq wn chj co ccv wbr wa c0h cif breq1 anbi2d eleq1 imbi12d
    wi imbi2d h0elch elimel atcvat2i dedth 3impib ) ADEZBFEZCFEZBCGHZABCIJZKLZM
    ZAFEZTZUGUHUIMZUOTUPUJUGANOZUKKLZMZUQFEZTZTANAUQGZUOVAUPVBUMUSUNUTVBULURUJA
    UQUKKPQAUQFRSUAUQBCANDUBUCUDUEUF $.
    $( [29-Jun-2004] $)

