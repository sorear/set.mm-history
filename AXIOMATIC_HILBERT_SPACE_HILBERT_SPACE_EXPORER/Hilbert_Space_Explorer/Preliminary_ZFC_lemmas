$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                  Preliminary ZFC lemmas
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( To convert to pure ZFC, put the definitions of ` +h ` , ` .h ` ,
     and ` .ih ` here. $)

  $( Define the function for the norm of a vector of Hilbert space.  See
     ~ normval for its value and ~ normcl for its closure.  Theorems
     ~ norm-i-i , ~ norm-ii-i , and ~ norm-iii-i show it has the expected
     properties of a norm.  In the literature, the norm of ` A ` is usually
     written "|| ` A ` ||", but we use function notation to take advantage of
     our existing theorems about functions.  Definition of norm in [Beran]
     p. 96. $)
  df-hnorm $a |- normh = ( x e. dom dom .ih |-> ( sqr ` ( x .ih x ) ) ) $.

  $( Define base set of Hilbert space, for use if we want to develop Hilbert
     space independently from the axioms (see comments in ~ ax-hilex ).  Note
     that ` ~H ` is considered a primitive in the Hilbert space axioms below,
     and we don't use this definition outside of this section.  This definition
     can be proved independently from those axioms as as theorem ~ hhba . $)
  df-hba $a |- ~H = ( BaseSet ` <. <. +h , .h >. , normh >. ) $.

  $( Define the zero vector of Hilbert space.  Note that ` 0vec ` is considered
     a primitive in the Hilbert space axioms below, and we don't use this
     definition outside of this section.  It is proved from the axioms as
     theorem ~ hh0v . $)
  df-h0v $a |- 0h = ( 0vec ` <. <. +h , .h >. , normh >. ) $.

  ${
    $d x y $.
    $( Define vector subtraction.  See ~ hvsubvali for its value and ~ hvsubcli
       for its closure. $)
    df-hvsub $a |- -h = ( x e. ~H , y e. ~H |-> ( x +h ( -u 1 .h y ) ) ) $.
  $}

  ${
    $d x y z f w $.
    $( Define the limit relation for Hilbert space.  See ~ hlimi for its
       relational expression.  Note that ` f : NN --> ~H ` is an infinite
       sequence of vectors, i.e. a mapping from integers to vectors.
       Definition of converge in [Beran] p. 96. $)
    df-hlim $a |- ~~>v = { <. f , w >. | ( ( f : NN --> ~H /\ w e. ~H ) /\
          A. x e. RR+ E. y e. NN A. z e. ( ZZ>= ` y )
                ( normh ` ( ( f ` z ) -h w ) ) < x ) } $.
  $}

  ${
    $d x y z w f $.
    $( Define the set of Cauchy sequences on a Hilbert space.  See ~ hcau for
       its membership relation.  Note that ` f : NN --> ~H ` is an infinite
       sequence of vectors, i.e. a mapping from integers to vectors.
       Definition of Cauchy sequence in [Beran] p. 96. $)
    df-hcau $a |- Cauchy = { f e. ( ~H ^m NN ) |
          A. x e. RR+ E. y e. NN A. z e. ( ZZ>= ` y )
             ( normh ` ( ( f ` y ) -h ( f ` z ) ) ) < x } $.
  $}

  ${
    h2h.1 $e |- U = <. <. +h , .h >. , normh >. $.
    h2h.2 $e |- U e. NrmCVec $.
    $( The group (addition) operation of Hilbert space. $)
    h2hva $p |- +h = ( +v ` U ) $=
      ( cva csm cop cno cpv cfv c1st eqid vafval opex op1st fveq2i cvv wcel cnv
      w3a eqeltrri nvex ax-mp simp1i 3eqtrri eqtr4i ) DDEFZGFZHIZAHIUHUGJIZJIUF
      JIDUGUHUHKLUIUFJUFGDEMNODEDPQZEPQZGPQZUGRQUJUKULSAUGRBCTEDGUAUBUCNUDAUGHB
      OUE $.
      $( [31-May-2008] $)

    $( The scalar product operation of Hilbert space. $)
    h2hsm $p |- .h = ( .s ` U ) $=
      ( csm cva cop cno cns cfv c1st c2nd eqid smfval opex op1st fveq2i cvv cnv
      wcel cvc wa eqeltrri nvvcop vcex mp2b simpli simpri op2nd 3eqtrri eqtr4i
      ) DEDFZGFZHIZAHIUMULJIZKIUKKIDUMULUMLMUNUKKUKGEDNOPEDEQSZDQSZULRSUKTSUOUP
      UAAULRBCUBDEGUCDEUDUEZUFUOUPUQUGUHUIAULHBPUJ $.
      $( [31-May-2008] $)

    $( The norm function of Hilbert space. $)
    h2hnm $p |- normh = ( norm ` U ) $=
      ( cnm cfv cva csm cop cno c2nd fveq2i eqid opex cvv wcel cnv w3a eqeltrri
      nmfval nvex ax-mp simp3i op2nd 3eqtrri ) ADEFGHZIHZDEZUFJEIAUFDBKUFUGUGLS
      UEIFGMFNOZGNOZINOZUFPOUHUIUJQAUFPBCRGFITUAUBUCUD $.
      $( [5-Jun-2008] $)

    ${
      $d x y U $.
      h2h.4 $e |- ~H = ( BaseSet ` U ) $.
      $( The vector subtraction operation of Hilbert space. $)
      h2hvs $p |- -h = ( -v ` U ) $=
        ( vx vy cmv chil cv c1 cneg csm co cva cmpt2 cnsb cfv df-hvsub cnv wcel
        wceq h2hva h2hsm eqid nvmfval ax-mp eqtr4i ) GEFHHEIJKFILMNMOZAPQZEFRAS
        TUIUHUACEFLANUIHDABCUBABCUCUIUDUEUFUG $.
        $( [23-Dec-2013] $) $( [6-Jun-2008] $)
    $}

    ${
      h2hm.4 $e |- ~H = ( BaseSet ` U ) $.
      h2hm.5 $e |- D = ( IndMet ` U ) $.
      $( The base set for the metric for Hilbert space. $)
      h2hmetba $p |- ~H = dom dom D $=
        ( chil imsbai ) ABGEFDH $.
        $( [6-Jun-2008] $)

      $( Value of the distance function of the metric space of Hilbert
         space. $)
      h2hmetdval $p |- ( ( A e. ~H /\ B e. ~H )
                  -> ( A D B ) = ( normh ` ( A -h B ) ) ) $=
        ( cnv wcel chil co cmv cno cfv wceq h2hvs h2hnm imsdval mp3an1 ) DIJAKJ
        BKJABCLABMLNOPFABCDMNKGDEFGQDEFRHST $.
        $( [6-Jun-2008] $)
    $}
  $}

  ${
    $d f j k x D $.
    h2hc.1 $e |- U = <. <. +h , .h >. , normh >. $.
    h2hc.2 $e |- U e. NrmCVec $.
    h2hc.3 $e |- ~H = ( BaseSet ` U ) $.
    h2hc.4 $e |- D = ( IndMet ` U ) $.
    $( The Cauchy sequences of Hilbert space.  (Revised by Mario Carneiro,
       13-May-2014.) $)
    h2hcau $p |- Cauchy = ( ( Cau ` D ) i^i ( ~H ^m NN ) ) $=
      ( vj vf vk vx cv cfv co clt wral cn crp chil wcel wa cmv cno wbr cuz wrex
      cmap crab cab ccau cca df-rab df-hcau elin ancom wf wb hlex nnex elmap c1
      cin imsbai nnuz cnv cme imsmet mp1i cz 1z a1i eqidd id iscauf wceq adantr
      ffvelrn uztrn2 sylan2 anassrs h2hmetdval syl2anc breq1d ralbidva rexbidva
      ralbidv bitrd sylbi pm5.32i 3bitri abbi2i 3eqtr4i ) GKZHKZLZIKZWMLZUAMUBL
      ZJKZNUCZIWLUDLZOZGPUEZJQOZHRPUFMZUGWMXDSZXCTZHUHUIAUJLZXDVAZXCHXDUKJGIHUL
      XFHXHWMXHSWMXGSZXETXEXITXFWMXGXDUMXIXEUNXEXIXCXEPRWMUOZXIXCUPRPWMBREUQURU
      SXJXIWNWPAMZWRNUCZIWTOZGPUEZJQOXCXJJWPWNAGIWMUTRPABREFDVBVCBVDSAVESXJDABF
      VFVGUTVHSXJVIVJXJWOPSZTWPVKXJWLPSZTZWNVKXJVLVMXJXNXBJQXJXMXAGPXQXLWSIWTXQ
      WOWTSZTZXKWQWRNXSWNRSZWPRSZXKWQVNXQXTXRPRWLWMVPVOXJXPXRYAXPXRTXJXOYAUTWOW
      LPVCVQPRWOWMVPVRVSWNWPABCDEFVTWAWBWCWDWEWFWGWHWIWJWK $.
      $( [13-May-2014] $) $( [6-Jun-2008] $)
  $}

  ${
    $d f x y J $.  $d f j k x y D $.
    h2hl.1 $e |- U = <. <. +h , .h >. , normh >. $.
    h2hl.2 $e |- U e. NrmCVec $.
    h2hl.3 $e |- ~H = ( BaseSet ` U ) $.
    h2hl.4 $e |- D = ( IndMet ` U ) $.
    h2hl.5 $e |- J = ( MetOpen ` D ) $.
    $( The limit sequences of Hilbert space.  (Revised by Mario Carneiro,
       13-May-2014.) $)
    h2hlm $p |- ~~>v = ( ( ~~>t ` J ) |` ( ~H ^m NN ) ) $=
      ( vf vx vk vy vj chli cfv chil cn cv wcel wa clm cmap co cres cmv cno clt
      wbr cuz wral wrex crp df-hlim relopabi relres cop hlex elmap anbi1i df-br
      wf nnex c1 cvv h2hmetba cnv cme imsmet mp1i vex a1i cz 1z eqidd id lmmbrf
      nnuz wb uztrn2 wceq ffvelrn h2hmetdval sylan breq1d an32s sylan2 ralbidva
      anassrs rexbidva ralbidv bitrd syl5bbr pm5.32i bitr2i anass ancom 3bitr4i
      pm5.32da copab eleq2i opabid bitri opelres eqrelriiv ) IJNCUAOZPQUBUCZUDZ
      QPIRZVAZJRZPSZTZKRZXHOZXJUEUCUFOZLRZUGUHZKMRZUIOZUJZMQUKZLULUJZTZIJNLMKJI
      UMZUNXEXFUOYCXHXJUPZXESZXHXFSZTZYENSZYEXGSXIXKYBTZTZYGYFTZYCYHYLXIYFTYKYG
      XIYFPQXHBPFUQVBURUSXIYFYJYFXHXJXEUHZXIYJXHXJXEUTXIYMXKXNXJAUCZXPUGUHZKXSU
      JZMQUKZLULUJZTYJXILXNAXJMKXHCVCVDPQABDEFGVEHBVFSAVGSXIEABGVHVIXJVDSXIJVJZ
      VKVQVCVLSXIVMVKXIXMQSZTZXNVNXIVOVPXIXKYRYBXLYQYALULXLYPXTMQXLXRQSZTYOXQKX
      SXLUUBXMXSSZYOXQVRZUUBUUCTXLYTUUDVCXMXRQVQVSXIYTXKUUDUUAXKTYNXOXPUGUUAXNP
      SXKYNXOVTQPXMXHWAXNXJABDEFGWBWCWDWEWFWHWGWIWJWRWKWLWMWNXIXKYBWOYFYGWPWQYI
      YEYCIJWSZSYCNUUEYEYDWTYCIJXAXBXHXJXEXFYSXCWQXD $.
      $( [13-May-2014] $) $( [6-Jun-2008] $)
  $}

