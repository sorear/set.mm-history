$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Orthocomplements
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Define orthogonal complement of a subset (usually a subspace) of Hilbert
       space.  The orthogonal complement is the set of all vectors orthogonal
       to all vectors in the subset.  See ~ ocval and ~ chocvali for its
       value.  Textbooks usually denote this unary operation with the symbol
       ` _|_ ` as a small superscript, although Mittelstaedt uses the symbol as
       a prefix operation.  Here we define a function (prefix operation)
       ` _|_ ` rather than introducing a new syntactical form.  This lets us
       take advantage of the theorems about functions that we already have
       proved under set theory.  Definition of [Mittelstaedt] p. 9. $)
    df-oc $a |- _|_ = ( x e. ~P ~H |->
                        { y e. ~H | A. z e. x ( y .ih z ) = 0 } ) $.
  $}

  $( Define the zero for closed subspaces of Hilbert space.  See ~ h0elch for
     closure law. $)
  df-ch0 $a |- 0H = { 0h } $.

  $( Membership in zero for closed subspaces of Hilbert space. $)
  elch0 $p |- ( A e. 0H <-> A = 0h ) $=
    ( c0h wcel c0v csn wceq df-ch0 eleq2i chil ax-hv0cl elexi elsnc2 bitri ) AB
    CADEZCADFBNAGHADDIJKLM $.
    $( [6-Apr-2001] $)

  $( The zero subspace is a closed subspace.  Part of Proposition 1 of
     [Kalmbach] p. 65. $)
  h0elch $p |- 0H e. CH $=
    ( c0h c0v csn cch df-ch0 hsn0elch eqeltri ) ABCDEFG $.
    $( [30-May-1999] $)

  $( The zero subspace is a subspace of Hilbert space. $)
  h0elsh $p |- 0H e. SH $=
    ( c0h h0elch chshii ) ABC $.
    $( [2-Jun-2004] $)

  ${
    hhss.1 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    $( The vector addition operation on a subspace. $)
    hhssva $p |- ( +h |` ( H X. H ) ) = ( +v ` W ) $=
      ( cpv cfv c1st cva cxp cres csm cc cop eqid vafval fveq2i opex op1st cabl
      cno wcel eqtri cvv hilabl resexg ax-mp 3eqtrri ) BDEZBFEZFEGAAHZIZJKAHIZL
      ZFEUJBUGUGMNUHULFUHULSAIZLZFEULBUNFCOULUMUJUKPQUAOUJUKGRTUJUBTUCGUIRUDUEQ
      UF $.
      $( [8-Apr-2008] $)

    $( The scalar multiplication operation on a subspace. $)
    hhsssm $p |- ( .h |` ( CC X. H ) ) = ( .s ` W ) $=
      ( cns cfv c1st c2nd cva cxp cres csm cop eqid smfval cno fveq2i opex cabl
      cc wcel op1st eqtri cvv hilabl resexg ax-mp hvmulex resex op2nd 3eqtrri )
      BDEZBFEZGEHAAIZJZKSAIZJZLZGEUPUKBUKMNULUQGULUQOAJZLZFEUQBUSFCPUQURUNUPQUA
      UBPUNUPHRTUNUCTUDHUMRUEUFKUOUGUHUIUJ $.
      $( [8-Apr-2008] $)

    $( The norm operation on a subspace. $)
    hhssnm $p |- ( normh |` H ) = ( norm ` W ) $=
      ( cnm cfv c2nd cva cxp cres csm cc cop cno eqid nmfval fveq2i chil cr cvv
      wcel opex wf normf ax-hilex fex mp2an resex op2nd 3eqtrri ) BDEZBFEGAAHIZ
      JKAHIZLZMAIZLZFEUNBUJUJNOBUOFCPUMUNUKULUAMAQRMUBQSTMSTUCUDQRSMUEUFUGUHUI
      $.
      $( [8-Apr-2008] $)
  $}

  ${
    $d x y H $.
    hhssabl.1 $e |- H e. SH $.
    $( Abelian group property of subspace addition. $)
    hhssabli $p |- ( +h |` ( H X. H ) ) e. AbelOp $=
      ( vx vy cva cxp cres cgr wcel wss cfv c0v csm c1 chil ax-mp eqid wceq co
      cc w3a c2nd cneg csn cvv ccnv ccom cabl hilabl ablogrpo cop cno hhba hhva
      csubg bafval cgi hilid eqcomi cnv cgn hhnv hhsm invfval shssii cv shaddcl
      csh mp3an1 sh0 wfn wf ax-hfvmul ffn ax-1cn negcli mp3an12 shmulcl eqeltrd
      curry1val issubgi issubg mpbi simp2i xpss12 mp2an ax-hfvadd fdmi sseqtr4i
      cdm ssdmres wa sheli ax-hvcom syl2an ovres ancoms 3eqtr4d isabloi ) CDEAA
      FZGZAEHIZXAHIZXAEJZXAEUOKIXBXCXDUACDLEXAMUBNUCZUDUEFGUFUGZOAEUHIXBUIEUJPE
      MUKULUKZEOXGXGQZUMXGXHUNZUPEUQKLURUSXGUTIXFEVAKRXGXHVBMXGEXFXIXGXHVCXFQZV
      DPABVEZXAQAVHIZCVFZAIZDVFZAIZXMXOESZAIBXMXOAVGVIXLLAIBAVJPXNXMXFKZXEXMMSZ
      AMTOFZVKZXETIZXNXRXSRXTOMVLYAVMXTOMVNPNVOVPZTOXEXMAMXFXJVTVQXLYBXNXSAIBYC
      XEXMAVRVQVSWAEXAWBWCWDWTEWJZJXAWJWTRWTOOFZYDAOJZYFWTYEJXKXKAOAOWEWFYEOEWG
      WHWIWTEWKWCXNXPWLXQXOXMESZXMXOXASXOXMXASZXNXMOIXOOIXQYGRXPXMABWMXOABWMXMX
      OWNWOXMXOAAEWPXPXNYHYGRXOXMAAEWPWQWRWS $.
      $( [23-Dec-2013] $) $( [9-Apr-2008] $)
  $}

  $( Abelian group property of subspace addition. $)
  hhssabl $p |- ( H e. SH -> ( +h |` ( H X. H ) ) e. AbelOp ) $=
    ( csh wcel cva cxp cres cabl chil cif wceq xpeq1 xpeq2 eqtrd reseq2d eleq1d
    helsh elimel hhssabli dedth ) ABCZDAAEZFZGCDTAHIZUCEZFZGCAHAUCJZUBUEGUFUAUD
    DUFUAUCAEUDAUCAKAUCUCLMNOUCAHBPQRS $.
    $( [9-Apr-2008] $)

  ${
    $d x y z H $.
    hhssnvt.1 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    ${
      hhssnv.2 $e |- H e. SH $.
      $( Normed complex vector space property of a subspace. $)
      hhssnv $p |- W e. NrmCVec $=
        ( vx vy csm cc cva cno c0v wcel wss wceq chil mp2an cfv co ovres eqtrd
        vz cxp cres cabl cgr hhssabli ablogrpo cdm shssii xpss12 ax-hfvadd fdmi
        ax-mp sseqtr4i ssdmres mpbi grporn cgi csh sh0 ax-hv0cl hvaddid2i eqtri
        wb eqid grpoid mpbir cop wf wfn crn df-f ax-hfvmul ssid fnssres cv wrex
        ovelrn wa shmulcl mp3an1 eqeltrd eleq1 syl5ibrcom rexlimivv sylbi ssriv
        ffn mpbir2an c1 ax-1cn mpan sheli ax-hvmulid syl w3a ax-hvdistr1 syl3an
        id 3adant1 oveq2d shaddcl 3impb 3adant3 3adant2 oveq12d syl2anc 3eqtr4d
        sylan2 caddc ax-hvdistr2 addcl sylan 3impa cmul ax-hvmulass mulcl isvci
        syl3an3 normf fssres cc0 fvres eqeq1d norm-i bitrd biimpa cabs norm-iii
        cr fveq2d adantl cle wbr norm-ii syl2an oveqan12d 3brtr4d isnvi ) EFGHA
        UBZUCZBIAAUBZUCZJAUCZAKUUCAUUCUDLUUCUELZADUFZUUCUGUMZUUBIUHZMUUCUHUUBNU
        UBOOUBZUUHAOMZUUJUUBUUIMADUIZUUKAOAOUJPUUIOIUKULUNUUBIUOUPZUQZKUUCURQZN
        ZKKUUCRZKNZUUPKKIRZKKALZUUSUUPUURNAUSLZUUSDAUTUMZUVAKKAAISPKVAVBVCUUEUU
        SUUOUUQVDUUGUVAKUUNUUCAUUMUUNVEVFPVGEFUAUUAUUCUUCUUAVHZAUUFUULYTAUUAVIU
        UAYTVJZUUAVKZAMYTAUUAVLGHOUBZVJZYTUVEMZUVCUVEOGVIUVFVMUVEOGWHUMHHMUUJUV
        GHVNUUKHHAOUJPUVEYTGVOPZUAUVDAUAVPZUVDLZUVIEVPZFVPZUUARZNZFAVQEHVQZUVIA
        LZUVCUVJUVOVDUVHEFHAUVIUUAVRUMUVNUVPEFHAUVKHLZUVLALZVSZUVPUVNUVMALUVSUV
        MUVKUVLGRZAUVKUVLHAGSUUTUVQUVRUVTALDUVKUVLAVTWAWBUVIUVMAWCWDWEWFWGWIUVK
        ALZWJUVKUUARZWJUVKGRZUVKWJHLUWAUWBUWCNWKWJUVKHAGSWLUWAUVKOLZUWCUVKNUVKA
        DWMZUVKWNWOTUVLHLZUWAUVPWPZUVLUVKUVIIRZGRZUVLUVKGRZUVLUVIGRZIRZUVLUVKUV
        IUUCRZUUARZUVLUVKUUARZUVLUVIUUARZUUCRZUWFUWFUWAUWDUVPUVIOLUWIUWLNUWFWSU
        WEUVIADWMUVLUVKUVIWQWRUWGUWNUVLUWHUUARZUWIUWGUWMUWHUVLUUAUWAUVPUWMUWHNU
        WFUVKUVIAAISWTXAUWFUWAUVPUWRUWINZUWAUVPVSUWFUWHALZUWSUUTUWAUVPUWTDUVKUV
        IAXBWAUVLUWHHAGSXIXCTUWGUWQUWJUWKUUCRZUWLUWGUWOUWJUWPUWKUUCUWFUWAUWOUWJ
        NZUVPUVLUVKHAGSZXDUWFUVPUWPUWKNUWAUVLUVIHAGSXEXFUWGUWJALZUWKALZUXAUWLNU
        WFUWAUXDUVPUUTUWFUWAUXDDUVLUVKAVTWAZXDUWFUVPUXEUWAUUTUWFUVPUXEDUVLUVIAV
        TWAXEUWJUWKAAISXGTXHUWFUVIHLZUWAWPZUVLUVIXJRZUVKGRZUWJUVIUVKGRZIRZUXIUV
        KUUARZUWOUVIUVKUUARZUUCRZUWAUWFUXGUWDUXJUXLNUWEUVLUVIUVKXKXSUWFUXGUWAUX
        MUXJNZUWFUXGVSZUXIHLUWAUXPUVLUVIXLUXIUVKHAGSXMXNUXHUXOUWJUXKUUCRZUXLUXH
        UWOUWJUXNUXKUUCUWFUWAUXBUXGUXCXEUXGUWAUXNUXKNUWFUVIUVKHAGSWTZXFUXHUXDUX
        KALZUXRUXLNUWFUWAUXDUXGUXFXEUXGUWAUXTUWFUUTUXGUWAUXTDUVIUVKAVTWAZWTUWJU
        XKAAISXGTXHUXHUVLUVIXORZUVKGRZUVLUXKGRZUYBUVKUUARZUVLUXNUUARZUWAUWFUXGU
        WDUYCUYDNUWEUVLUVIUVKXPXSUWFUXGUWAUYEUYCNZUXQUYBHLUWAUYGUVLUVIXQUYBUVKH
        AGSXMXNUXHUYFUVLUXKUUARZUYDUXHUXNUXKUVLUUAUXSXAUWFUXGUWAUYHUYDNZUXGUWAV
        SUWFUXTUYIUYAUVLUXKHAGSXIXCTXHUVBVEXROYJJVIUUJAYJUUDVIXTUUKOYJAJYAPUWAU
        VKUUDQZYBNZUVKKNZUWAUYKUVKJQZYBNZUYLUWAUYJUYMYBUVKAJYCZYDUWAUWDUYNUYLVD
        UWEUVKYEWOYFYGUWFUWAVSZUWJJQZUVLYHQZUYMXORZUWOUUDQZUYRUYJXORUWAUWFUWDUY
        QUYSNUWEUVLUVKYIXIUYPUYTUWJUUDQZUYQUYPUWOUWJUUDUXCYKUYPUXDVUAUYQNUXFUWJ
        AJYCWOTUYPUYJUYMUYRXOUWAUYJUYMNUWFUYOYLXAXHUWAUVRVSZUVKUVLIRZJQZUYMUVLJ
        QZXJRZUVKUVLUUCRZUUDQZUYJUVLUUDQZXJRYMUWAUWDUVLOLVUDVUFYMYNUVRUWEUVLADW
        MUVKUVLYOYPVUBVUHVUCUUDQZVUDVUBVUGVUCUUDUVKUVLAAISYKVUBVUCALZVUJVUDNUUT
        UWAUVRVUKDUVKUVLAXBWAVUCAJYCWOTUWAUVRUYJUYMVUIVUEXJUYOUVLAJYCYQYRCYS $.
        $( [26-Mar-2008] $)
    $}

    $( Normed complex vector space property of a subspace. $)
    hhssnvt $p |- ( H e. SH -> W e. NrmCVec ) $=
      ( csh wcel cnv cva c0h cif cxp cres csm cc cop wceq xpeq1 reseq2d opeq12d
      cno xpeq2 eqtrd reseq2 syl5eq eleq1d eqid h0elsh elimel hhssnv dedth ) AD
      EZBFEGUJAHIZUKJZKZLMUKJZKZNZSUKKZNZFEAHAUKOZBURFUSBGAAJZKZLMAJZKZNZSAKZNU
      RCUSVDUPVEUQUSVAUMVCUOUSUTULGUSUTUKAJULAUKAPAUKUKTUAQUSVBUNLAUKMTQRAUKSUB
      RUCUDUKURURUEAHDUFUGUHUI $.
      $( [9-Apr-2008] $)
  $}

  ${
    $d x y z H $.
    hhsst.1 $e |- U = <. <. +h , .h >. , normh >. $.
    hhsst.2 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    $( A member of ` SH ` is a subspace. $)
    hhsst $p |- ( H e. SH -> W e. ( SubSp ` U ) ) $=
      ( csh wcel cnv cva cxp cres wss csm cc cno w3a wa css cfv resss hhnv hhva
      hhssnvt 3pm3.2i jctir wb hhssva hhsm hhsssm hhnm hhssnm eqid isssp sylibr
      ax-mp ) BFGZCHGZIBBJZKZILZMNBJZKZMLZOBKZOLZPZQZCARSZGZUPUQVFBCEUCUTVCVEIU
      RTMVATOBTUDUEAHGVIVGUFADUAVBMAUSIVHVDOCADUBBCEUGADUHBCEUIADUJBCEUKVHULUMU
      OUN $.
      $( [6-Apr-2008] $)

    ${
      hhssp3.3 $e |- W e. ( SubSp ` U ) $.
      hhssp3.4 $e |- H C_ ~H $.
      $( Lemma for ~ hhsssh . $)
      hhshsslem1 $p |- H = ( BaseSet ` W ) $=
        ( cfv cpv eqid cdm cxp wcel ax-mp cva cres c1st eqtri wss chil cba wceq
        crn bafval cgr cnv css sspnv mp2an nvgrp grporndm csm cc cop cno fveq2i
        hhnv vafval opex op1st cabl cvv hilabl resexg xpss12 ax-hfvadd sseqtr4i
        dmeqi fdmi ssdmres mpbi dmxpid 3eqtri eqcomi ) CUAHZBVOCIHZUCZBCVPVOVOJ
        VPJZUDVQVPKZKZBBLZKBVPUEMZVQVTUBCUFMZWBAUFMCAUGHZMWCADUQFAWDCWDJUHUICVP
        VRUJNVPUKNVSWAVSOWAPZKZWAVPWEVPWEULUMBLPZUNZUOBPZUNZIHZWECWJIEUPWKWJQHZ
        QHZWEWJWKWKJURWMWHQHWEWLWHQWHWIWEWGUSUTUPWEWGOVAMWEVBMVCOWAVAVDNUTRRRVH
        WAOKZSWFWAUBWATTLZWNBTSZWPWAWOSGGBTBTVEUIWOTOVFVIVGWAOVJVKRVHBVLVMRVN
        $.
        $( [10-Apr-2008] $)

      $( Lemma for ~ hhsssh . $)
      hhshsslem2 $p |- H e. SH $=
        ( vx vy wcel c0v wa cv cva co wral cc cfv wceq eqid csh chil issh2 cn0v
        wss csm cnv css hhnv hh0v mp2an cba sspnv nvzcl ax-mp hhshsslem1 eqcomi
        sspz eleqtri eqeltrri pm3.2i hhva sspgval mpanl12 nvgcl mp3an1 eqeltrrd
        cpv rgen2a cns hhsm sspsval nvscl rgen2 mpbir2an ) BUAJBUBUEZKBJZLHMZIM
        ZNOZBJZIBPHBPZVRVSUFOZBJZIBPHQPZLHIBUCVPVQGCUDRZKBAUGJZCAUHRZJZWFKSADUI
        ZFWFAWHCKADUJWFTZWHTZURUKWFCULRZBCUGJZWFWMJWGWIWNWJFAWHCWLUMUKZCWMWFWMT
        WKUNUOBWMABCDEFGUPZUQUSUTVAWBWEWAHIBVRBJZVSBJZLZVRVSCVHRZOZVTBWGWIWSXAV
        TSWJFVRVSAWTNWHCBWPADVBWTTZWLVCVDWNWQWRXABJWOVRVSCWTBWPXBVEVFVGVIWDHIQB
        VRQJZWRLZVRVSCVJRZOZWCBWGWIXDXFWCSWJFVRVSXEUFAWHCBWPADVKXETZWLVLVDWNXCW
        RXFBJWOVRVSXECBWPXGVMVFVGVNVAVO $.
        $( [6-Apr-2008] $)
    $}

    $( The predicate " ` H ` is a subspace of Hilbert space." $)
    hhsssh $p |- ( H e. SH <-> ( W e. ( SubSp ` U ) /\ H C_ ~H ) ) $=
      ( csh wcel chil cva cxp cres csm cop cno wceq xpeq2 reseq2d opeq12d ax-mp
      cc css cfv wss wa hhsst shss jca cif eleq1 eqid xpeq1 eqtrd reseq2 syl5eq
      eleq1d sseq1 anbi12d wfn wf ax-hfvadd ffn fnresdm ax-hfvmul opeq12i normf
      cr eqtr4i hhnv sspid eqeltri ssid pm3.2i elimhyp simpli simpri hhshsslem2
      cnv dedth impbii ) BFGZCAUAUBZGZBHUCZUDZVTWBWCABCDEUEBUFUGWDVTWDBHUHZFGBH
      BWEFUIAWEIWEWEJZKZLTWEJZKZMZNWEKZMZDWLUJWLWAGZWEHUCZWDWMWNUDIHHJZKZLTHJZK
      ZMZNHKZMZWAGZHHUCZUDBHBWEOZWBWMWCWNXDCWLWAXDCIBBJZKZLTBJZKZMZNBKZMWLEXDXI
      WJXJWKXDXFWGXHWIXDXEWFIXDXEWEBJWFBWEBUKBWEWEPULQXDXGWHLBWETPQRBWENUMRUNUO
      BWEHUPUQHWEOZXBWMXCWNXKXAWLWAXKWSWJWTWKXKWPWGWRWIXKWOWFIXKWOWEHJWFHWEHUKH
      WEWEPULQXKWQWHLHWETPQRHWENUMRUOHWEHUPUQXBXCXAAWAXAILMZNMAWSXLWTNWPIWRLIWO
      URZWPIOWOHIUSXMUTWOHIVASWOIVBSLWQURZWRLOWQHLUSXNVCWQHLVASWQLVBSVDNHURZWTN
      OHVFNUSXOVEHVFNVASHNVBSVDDVGAVQGAWAGADVHAWAWAUJVISVJHVKVLVMZVNWMWNXPVOVPV
      RVS $.
      $( [25-Mar-2008] $)
  $}

  ${
    $d x y H $.
    hhsssh2.1 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    $( The predicate " ` H ` is a subspace of Hilbert space." $)
    hhsssh2 $p |- ( H e. SH <-> ( W e. NrmCVec /\ H C_ ~H ) ) $=
      ( wcel cva csm cop cno cfv chil wss wa eqid cres cpv fveq2i c1st cvv c2nd
      eqtri csh css cnv hhsssh cxp cc w3a wb hhnv hhva vafval opex op1st hilabl
      cabl resexg ax-mp eqtr2i hhsm cns smfval hvmulex resex hhnm cnm nmfval cr
      op2nd normf ax-hilex fex mp2an isssp resss 3pm3.2i mpbiran2 anbi1i bitri
      wf ) AUADBEFGHGZUBIZDZAJKZLBUCDZWCLVTABVTMZCUDWBWDWCWBWDEAAUEZNZEKZFUFAUE
      ZNZFKZHANZHKZUGZVTUCDWBWDWNLUHVTWEUIWJFVTWGEWAWLHBVTWEUJBOIWGWJGZWLGZOIZW
      GBWPOCPWQWPQIZQIZWGWPWQWQMUKWSWOQIWGWRWOQWOWLWGWJULZUMZPWGWJEUODWGRDUNEWF
      UOUPUQZUMTTURVTWEUSBUTIWPUTIZWJBWPUTCPXCWRSIZWJXCWPXCMVAXDWOSIWJWRWOSXAPW
      GWJXBFWIVBVCVHTTURVTWEVDBVEIWPVEIZWLBWPVECPXEWPSIWLWPXEXEMVFWOWLWTHAJVGHV
      SJRDHRDVIVJJVGRHVKVLVCVHTURWAMVMUQWHWKWMEWFVNFWIVNHAVNVOVPVQVR $.
      $( [8-Apr-2008] $)

    ${
      hhssba.2 $e |- H e. SH $.
      $( The base set of a subspace. $)
      hhssba $p |- H = ( BaseSet ` W ) $=
        ( cva csm cop cno eqid csh wcel css cfv hhsst ax-mp shssii hhshsslem1 )
        EFGHGZABRIZCAJKBRLMKDRABSCNOADPQ $.
        $( [10-Apr-2008] $)

      $( The vector subtraction operation on a subspace. $)
      hhssvs $p |- ( -h |` ( H X. H ) ) = ( -v ` W ) $=
        ( cnsb cfv cmv cxp cres cva csm cop cno cnv wcel css wceq eqid hhnv csh
        hhsst ax-mp hhssba hhvs sspm mp2an eqcomi ) BEFZGAAHIZJKLMLZNOBUJPFZOZU
        HUIQUJUJRZSATOULDUJABUMCUAUBUJUKUHGBAABCDUCUJUMUDUHRUKRUEUFUG $.
        $( [10-Apr-2008] $)

      $( Mapping of the vector subtraction operation on a subspace. $)
      hhssvsf $p |- ( -h |` ( H X. H ) ) : ( H X. H ) --> H $=
        ( cnv wcel cxp cmv cres wf hhssnv hhssba hhssvs nvmf ax-mp ) BEFAAGZAHP
        IZJABCDKBQAABCDLABCDMNO $.
        $( [10-Apr-2008] $)

      $( Inner product space property of a subspace. $)
      hhssph $p |- W e. CPreHil $=
        ( cva csm cop cno cphl wcel css cfv eqid hhph hhsst ax-mp sspph mp2an
        csh ) EFGHGZIJBTKLZJZBIJTTMZNASJUBDTABUCCOPTUABUAMQR $.
        $( [10-Apr-2008] $)
    $}

    ${
      hhssims.2 $e |- H e. SH $.
      hhssims.3 $e |- D = ( ( normh o. -h ) |` ( H X. H ) ) $.
      $( Induced metric of a subspace. $)
      hhssims $p |- D = ( IndMet ` W ) $=
        ( cno cmv ccom cxp cres cims cfv resco crn wss wceq wf hhssvsf ax-mp
        frn cores eqtr4i cnv wcel hhssnv hhssvs hhssnm eqid imsval 3eqtr4i ) GH
        IBBJZKZGBKZHULKZIZACLMZUMGUOIZUPGHULNUOOBPZUPURQULBUORUSBCDESULBUOUATGU
        OBUBTUCFCUDUEUQUPQBCDEUFUQCUOUNBCDEUGBCDUHUQUIUJTUK $.
        $( [10-Apr-2008] $)
    $}
  $}

  ${
    hhssims2.1 $e |- W =
   <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. , ( normh |` H ) >. $.
    hhssims2.3 $e |- D = ( IndMet ` W ) $.
    ${
      hhssims2.2 $e |- H e. SH $.
      $( Induced metric of a subspace. $)
      hhssims2 $p |- D = ( ( normh o. -h ) |` ( H X. H ) ) $=
        ( cims cfv cno cmv ccom cxp cres eqid hhssims eqtr4i ) ACGHIJKBBLMZEQBC
        DFQNOP $.
        $( [10-Apr-2008] $)

      $( Induced metric of a subspace. $)
      hhssmet $p |- D e. Met $=
        ( cnv wcel cme hhssnv imsmet ax-mp ) CGHAIHBCDFJACEKL $.
        $( [10-Apr-2008] $)

      $( The base set for the metric of a subspace. $)
      hhssmetba $p |- H = dom dom D $=
        ( hhssba hhssnv imsbai ) ACBBCDFGEBCDFHI $.
        $( [10-Apr-2008] $)

      $( Value of the distance function of the metric space of a subspace. $)
      hhssmetdval $p |- ( ( A e. H /\ B e. H )
         -> ( A D B ) = ( normh ` ( A -h B ) ) ) $=
        ( wcel wa co cmv cxp cres cno cfv cnv wceq hhssnv mp3an1 hhssba imsdval
        hhssvs hhssnm ovres fveq2d csh shsubcl fvres syl 3eqtrd ) ADIZBDIZJZABC
        KZABLDDMNZKZODNZPZABLKZURPZUTOPZEQIULUMUOUSRDEFHSABCEUPURDDEFHUADEFHUCD
        EFUDGUBTUNUQUTURABDDLUEUFUNUTDIZVAVBRDUGIULUMVCHABDUHTUTDOUIUJUK $.
        $( [10-Apr-2008] $)
    $}

    ${
      $d f j k x y D $.  $d f j k x y H $.
      hhsscms.3 $e |- H e. CH $.
      $( The induced metric of a closed subspace is complete.  (Revised by
         Mario Carneiro, 14-May-2014.) $)
      hhsscms $p |- D e. CMet $=
        ( vf vx cfv eqid cca wcel cn wf chli wbr chil cop ax-mp a1i copn chshii
        hhssmetba hhssmet cv wa clm cdm cno cmv ccom ccau wrex cmap co cxp cres
        simpl hhssims2 fveq2i syl6eleq cme wb hilmet simpr causs sylancr mpbird
        wss chssii fss sylancl ax-hilex elmap sylibr cin cva hhims hhcau eleq2i
        nnex csm elin bitri sylanbrc ax-hcompl breldm rexlimivw 3syl wfun hlimf
        vex ffun funfvbrb sylib hhlm resss eqsstri ssbri syl c1 cch csubsp wceq
        hilmetba subtopmet mp2an eqcomi nnuz ctop opntop fvex chlimi syl3anc cz
        1z lmss mpbid iscms3i ) AGAUAIZBABCDEBFUBZUCXTJZABCDEYAUDGUEZAKIZLZMBYC
        NZUFZYCYCOIZXTUGIZPZYCYIUHLYGYCYHUIUJUKZUAIZUGIZPZYJYGYCYHOPZYNYGYCOUHZ
        LZYOYGYCULLZYCHUEZOPZHQUMYQYGYCYKKIZLZYCQMUNUOZLZYRYGUUBYCYKBBUPUQZKIZL
        ZYGYCYDUUFYEYFURAUUEKABCDEYAUSZUTVAYGYKVBLZYFUUBUUGVCYKYKJZVDZYEYFVEZYK
        YCBVFVGVHYGMQYCNZUUDYGYFBQVIZUUMUULBFVJZMBQYCVKVLQMYCVMWAVNVOYRYCUUAUUC
        VPZLUUBUUDUFULUUPYCYKVQWBRUIRZUUQJZYKUUQUURUUJVRZVSVTYCUUAUUCWCWDWEHYCW
        FYTYQHQYCYSOGWLZWGWHWIOWJZYQYOVCYPQONUVAWKYPQOWMSYCOWNSWOZOYMYCYHOYMUUC
        UQYMYKUUQYLUURUUSYLJZWPYMUUCWQWRWSWTYGYHYCYLXTXAXBBMBYLRXCIZXTUUIUUNUVD
        XTXDUUKUUOYKAYLXTQBUUHYKUUJXEUVCYBXFXGXHXIBXBLZYGFTZYLXJLZYGUUIUVGUUKYK
        YLUVCXKSTYGUVEYFYOYHBLUVFUULUVBYHYCBYCOXLXMXNXAXOLYGXPTUULXQXRYCYHYIUUT
        WGWTXS $.
        $( [14-May-2014] $) $( [10-Apr-2008] $)
    $}
  $}

  ${
    hhssbn.1 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    hhssbn.2 $e |- H e. CH $.
    $( Banach space property of a closed subspace. $)
    hhssbn $p |- W e. CBan $=
      ( cbn wcel cnv cims cfv cms eqid isbn chshii hhssnv hhsscms mpbir2an ) BE
      FBGFBHIZJFQBQKZLABCADMNQABCRDOP $.
      $( [10-Apr-2008] $)

    $( Hilbert space property of a closed subspace. $)
    hhsshl $p |- W e. CHil $=
      ( chl wcel cbn cphl ishl hhssbn chshii hhssph mpbir2an ) BEFBGFBHFBIABCDJ
      ABCADKLM $.
      $( [10-Apr-2008] $)
  $}

  ${
    $d x y z H $.  $d x y z A $.  $d x y B $.
    $( Value of orthogonal complement of a subset of Hilbert space.  (Revised
       by Mario Carneiro, 23-Dec-2013.) $)
    ocval $p |- ( H C_ ~H -> ( _|_ ` H ) =
        { x e. ~H | A. y e. H ( x .ih y ) = 0 } ) $=
      ( vz chil wss cpw wcel cort cfv cv csp cc0 wceq wral ax-hilex elpw2 raleq
      co crab rabbidv df-oc rabex fvmpt sylbir ) CEFCEGZHCIJAKBKLSMNZBCOZAETZNC
      EPQDCUGBDKZOZAETUIUFIUJCNUKUHAEUGBUJCRUADABUBUHAEPUCUDUE $.
      $( [23-Dec-2013] $) $( [7-Aug-2000] $)

    $( Membership in orthogonal complement of H subset. $)
    ocel $p |- ( H C_ ~H -> ( A e. ( _|_ ` H ) <-> ( A e. ~H /\
                  A. x e. H ( A .ih x ) = 0 ) ) ) $=
      ( vy chil wss cort cfv wcel cv csp co cc0 wceq wral wa ocval eleq2d oveq1
      crab eqeq1d ralbidv elrab syl6bb ) CEFZBCGHZIBDJZAJZKLZMNZACOZDETZIBEIBUH
      KLZMNZACOZPUEUFULBDACQRUKUODBEUGBNZUJUNACUPUIUMMUGBUHKSUAUBUCUD $.
      $( [7-Aug-2000] $)

    $( Membership in orthogonal complement of H subspace. $)
    shocel $p |- ( H e. SH -> ( A e. ( _|_ ` H ) <-> ( A e. ~H /\
                  A. x e. H ( A .ih x ) = 0 ) ) ) $=
      ( csh wcel chil wss cort cfv cv csp co cc0 wceq wral wa wb shss ocel syl
      ) CDECFGBCHIEBFEBAJKLMNACOPQCRABCST $.
      $( [9-Oct-1999] $)

    $( The orthogonal complement of a subspace is a subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    ocsh $p |- ( A C_ ~H -> ( _|_ ` A ) e. SH ) $=
      ( vx vy vz chil wss c0v wcel wa cv co wral cc csp wceq syl6 ralrimiv ocel
      cc0 wi cort cfv cva csm csh crab ssrab2 ocval sseq1d mpbiri ssel ax-hv0cl
      hi01 jctil mpbird jca ssel2 caddc ax-his2 3expa oveq12 0cn addid1i syl6eq
      sylan9eq ancoms sylan an32s ralimdva imdistand hvaddcl anim1i anbi12d an4
      r19.26 anbi2i bitr4i syl6bb 3imtr4d exp3a ralrimdv mul01 oveq2 syl5ibrcom
      ex cmul eqeq1d ad2antrl wb w3a ax-his3 sylibrd hvmulcl anbi2d anass issh2
      syl6bbr sylanbrc ) AEFZAUAUBZEFZGWTHZIBJZCJZUCKZWTHZCWTLZBWTLZXCXDUDKZWTH
      ZCWTLZBMLZIWTUEHWSXAXBWSXAXCXDNKSOCALZBEUFZEFXMBEUGWSWTXNEBCAUHUIUJWSXBGE
      HZGXDNKSOZCALZIWSXQXOWSXPCAWSXDAHXDEHZXPAEXDUKXDUMPQULUNCGARUOUPWSXHXLWSX
      GBWTWSXCWTHZXFCWTWSXSXDWTHZXFWSXCEHZXRIZXCDJZNKZSOZXDYCNKZSOZIZDALZIZXEEH
      ZXEYCNKZSOZDALZIZXSXTIZXFWSYJYBYNIYOWSYBYIYNWSYBYIYNTWSYBIYHYMDAWSYCAHZYB
      YHYMTZWSYQIZYCEHZYBYRAEYCUQZYBYTYRYBYTIZYHYMUUBYHYLYDYFURKZSYAXRYTYLUUCOX
      CXDYCUSUTYHUUCSSURKSYDSYFSURVASVBVCVDVEWEVFVGVHVIWEVJYBYKYNXCXDVKVLPWSYPY
      AYEDALZIZXRYGDALZIZIZYJWSXSUUEXTUUGDXCARDXDARZVMUUHYBUUDUUFIZIYJYAUUDXRUU
      FVNYIUUJYBYEYGDAVOVPVQVRDXEARVSVTWAQWSXKBMWSXCMHZXJCWTWSUUKXTXJWSUUKXRIZU
      UFIZXIEHZXIYCNKZSOZDALZIZUUKXTIZXJWSUUMUULUUQIUURWSUULUUFUUQWSUULUUFUUQTW
      SUULIYGUUPDAWSYQUULYGUUPTZYSYTUULUUTUUAYTUULIYGXCYFWFKZSOZUUPUUKYGUVBTYTX
      RUUKUVBYGXCSWFKZSOXCWBYGUVAUVCSYFSXCWFWCWGWDWHUULYTUUPUVBWIZUUKXRYTUVDUUK
      XRYTWJUUOUVASXCXDYCWKWGUTVFWLVGVHVIWEVJUULUUNUUQXCXDWMVLPWSUUSUUKUUGIUUMW
      SXTUUGUUKUUIWNUUKXRUUFWOWQDXIARVSVTWAQUPBCWTWPWR $.
      $( [7-Aug-2000] $)

    $( The orthogonal complement of a subspace is a subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    shocsh $p |- ( A e. SH -> ( _|_ ` A ) e. SH ) $=
      ( csh wcel chil wss cort cfv shss ocsh syl ) ABCADEAFGBCAHAIJ $.
      $( [10-Oct-1999] $)

    $( An orthogonal complement is a subset of Hilbert space. $)
    ocss $p |- ( A C_ ~H -> ( _|_ ` A ) C_ ~H ) $=
      ( chil wss cort cfv csh wcel ocsh shss syl ) ABCADEZFGKBCAHKIJ $.
      $( [9-Aug-2000] $)

    $( An orthogonal complement is a subset of Hilbert space. $)
    shocss $p |- ( A e. SH -> ( _|_ ` A ) C_ ~H ) $=
      ( csh wcel chil wss cort cfv shss ocss syl ) ABCADEAFGDEAHAIJ $.
      $( [11-Oct-1999] $)

    $( Contraposition law for orthogonal complement. $)
    occon $p |- ( ( A C_ ~H /\ B C_ ~H ) ->
                 ( A C_ B -> ( _|_ ` B ) C_ ( _|_ ` A ) ) ) $=
      ( vx vy chil wss wa cort cfv cv csp co wceq wral crab wi wal df-ral ocval
      wcel cc0 imim1d alimdv 3imtr4g a1d ralrimiv ss2rab sylibr adantl ad2antlr
      ssel ad2antrr 3sstr4d ex ) AEFZBEFZGZABFZBHIZAHIZFUQURGCJZDJZKLUAMZDBNZCE
      OZVCDANZCEOZUSUTURVEVGFZUQURVDVFPZCENVHURVICEURVIVAETURVBBTZVCPZDQVBATZVC
      PZDQVDVFURVKVMDURVLVJVCABVBUKUBUCVCDBRVCDARUDUEUFVDVFCEUGUHUIUPUSVEMUOURC
      DBSUJUOUTVGMUPURCDASULUMUN $.
      $( [8-Aug-2000] $)

    $( Double contraposition for orthogonal complement. $)
    occon2 $p |- ( ( A C_ ~H /\ B C_ ~H ) ->
              ( A C_ B -> ( _|_ ` ( _|_ ` A ) ) C_ ( _|_ ` ( _|_ ` B ) ) ) ) $=
      ( chil wss wa cort cfv ocss anim12i ancoms occon sylsyld ) ACDZBCDZEBFGZC
      DZAFGZCDZEZABDOQDQFGOFGDNMSNPMRBHAHIJABKOQKL $.
      $( [22-Jul-2001] $)

    ${
      occon2.1 $e |- A C_ ~H $.
      occon2.2 $e |- B C_ ~H $.
      $( Double contraposition for orthogonal complement. $)
      occon2i $p |- ( A C_ B ->
                   ( _|_ ` ( _|_ ` A ) ) C_ ( _|_ ` ( _|_ ` B ) ) ) $=
        ( chil wss cort cfv wi occon2 mp2an ) AEFBEFABFAGHGHBGHGHFICDABJK $.
        $( [9-Aug-2000] $)
    $}

    $( The zero vector belongs to an orthogonal complement of a Hilbert
       subspace. $)
    oc0 $p |- ( H e. SH -> 0h e. ( _|_ ` H ) ) $=
      ( csh wcel cort cfv c0v shocsh sh0 syl ) ABCADEZBCFJCAGJHI $.
      $( [11-Oct-1999] $)

    $( Members of a subset and its complement are orthogonal. $)
    ocorth $p |- ( H C_ ~H -> ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
                 ( A .ih B ) = 0 ) ) $=
      ( vx chil wss wcel cort cfv wa csp co cc0 wceq cv wral simplbda adantl wi
      ocel oveq2 eqeq1d rcla4v ad2antlr wb ssel2 ocss sseld imp orthcom sylibrd
      syl2an mpd anandis ex ) CEFZACGZBCHIZGZJABKLMNZUPUQUSUTUPUQJZUPUSJZJZBDOZ
      KLZMNZDCPZUTVBVGVAUPUSBEGZVGDBCTQRVCVGBAKLZMNZUTUQVGVJSUPVBVFVJDACVDANVEV
      IMVDABKUAUBUCUDVAAEGVHUTVJUEVBCEAUFUPUSVHUPUREBCUGUHUIABUJULUKUMUNUO $.
      $( [9-Aug-2000] $)

    $( Members of a subspace and its complement are orthogonal. $)
    shocorth $p |- ( H e. SH -> ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
                 ( A .ih B ) = 0 ) ) $=
      ( csh wcel chil wss cort cfv wa csp co cc0 wceq wi shss ocorth syl ) CDEC
      FGACEBCHIEJABKLMNOCPABCQR $.
      $( [10-Oct-1999] $)

    $( Inclusion in complement of complement.  Part of Proposition 1 of
       [Kalmbach] p. 65. $)
    ococss $p |- ( A C_ ~H -> A C_ ( _|_ ` ( _|_ ` A ) ) ) $=
      ( vy vx chil wss cort cfv cv wcel csp co cc0 wceq wral wa ocorth ralrimdv
      ssel exp3a jcad wb ocss ocel syl sylibrd ssrdv ) ADEZBAAFGZFGZUGBHZAIZUJD
      IZUJCHZJKLMZCUHNZOZUJUIIZUGUKULUOADUJRUGUKUNCUHUGUKUMUHIUNUJUMAPSQTUGUHDE
      UQUPUAAUBCUJUHUCUDUEUF $.
      $( [9-Aug-2000] $)

    $( Inclusion in complement of complement.  Part of Proposition 1 of
       [Kalmbach] p. 65. $)
    shococss $p |- ( A e. SH -> A C_ ( _|_ ` ( _|_ ` A ) ) ) $=
      ( csh wcel chil wss cort cfv shss ococss syl ) ABCADEAAFGFGEAHAIJ $.
      $( [10-Oct-1999] $)

    $( Members of orthogonal subspaces are orthogonal. $)
    shorth $p |- ( H e. SH -> ( G C_ ( _|_ ` H ) ->
        ( ( A e. G /\ B e. H ) -> ( A .ih B ) = 0 ) ) ) $=
      ( csh wcel cort cfv wss wa csp cc0 wceq ssel anim1d imp ancom chil sseld
      co sylib shocorth wb shss shocss anim12d orthcom syl mpbid sylan2 exp32 )
      DEFZCDGHZIZACFZBDFZJZABKTLMZUNUQJZULUPAUMFZJZURUSUTUPJZVAUNUQVBUNUOUTUPCU
      MANOPUTUPQUAULVAJZBAKTLMZURULVAVDBADUBPVCBRFZARFZJZVDURUCULVAVGULUPVEUTVF
      ULDRBDUDSULUMRADUESUFPBAUGUHUIUJUK $.
      $( [17-Oct-1999] $)

    $( Intersection of a Hilbert subspace and its complement.  Part of
       Proposition 1 of [Kalmbach] p. 65. $)
    ocin $p |- ( A e. SH -> ( A i^i ( _|_ ` A ) ) = 0H ) $=
      ( vx vy csh wcel cort cfv cin c0h cv wa c0v wceq chil csp co cc0 wi eleq1
      wral shocel oveq2 eqeq1d rcla4cv his6 biimpd sylan9r syl6bi com23 sh0 oc0
      imp3a jca anbi12d syl5ibrcom impbid elin elch0 3bitr4g eqrdv ) ADEZBAAFGZ
      HZIVABJZAEZVDVBEZKZVDLMZVDVCEVDIEVAVGVHVAVEVFVHVAVFVEVHVAVFVDNEZVDCJZOPZQ
      MZCATZKVEVHRCVDAUAVMVEVDVDOPZQMZVIVHVLVOCVDAVJVDMVKVNQVJVDVDOUBUCUDVIVOVH
      VDUEUFUGUHUIULVAVGVHLAEZLVBEZKVAVPVQAUJAUKUMVHVEVPVFVQVDLASVDLVBSUNUOUPVD
      AVBUQVDURUSUT $.
      $( [11-Oct-1999] $)
    $( Hilbert lattice contraposition law.  (Contributed by Mario Carneiro,
       18-May-2014.) $)
    occon3 $p |- ( ( A C_ ~H /\ B C_ ~H ) ->
                     ( A C_ ( _|_ ` B ) <-> B C_ ( _|_ ` A ) ) ) $=
      ( chil wss wa cort cfv ococss adantl wi occon sylan2 sstr2 sylsyld adantr
      ocss id syl2anr impbid ) ACDZBCDZEZABFGZDZBAFGZDZUBBUCFGZDZUDUGUEDZUFUAUH
      TBHIUATUCCDUDUIJBPAUCKLBUGUEMNUBAUEFGZDZUFUJUCDZUDTUKUAAHOUAUAUECDUFULJTU
      AQAPBUEKRAUJUCMNS $.
      $( [18-May-2014] $)
  $}

  $( A nonzero vector in the complement of a subspace does not belong to the
     subspace. $)
  ocnel $p |- ( ( H e. SH /\ A e. ( _|_ ` H ) /\ A =/= 0h ) -> -. A e. H ) $=
    ( csh wcel cort cfv c0v wne wn c0h wceq cin elin ocin eleq2d biimpd syl5bir
    wa wi exp3a com23 imp elch0 syl6ib necon3ad 3impia ) BCDZABEFZDZAGHABDZIUGU
    IRZUJAGUKUJAJDZAGKUGUIUJULSUGUJUIULUGUJUIULUJUIRABUHLZDZUGULABUHMUGUNULUGUM
    JABNOPQTUAUBAUCUDUEUF $.
    $( [10-Apr-2006] $)

  ${
    $d x y A $.
    chocval.1 $e |- A e. CH $.
    $( Value of the orthogonal complement of a Hilbert lattice element.  The
       orthogonal complement of ` A ` is the set of vectors that are orthogonal
       to all vectors in ` A ` . $)
    chocvali $p |- ( _|_ ` A ) = { x e. ~H | A. y e. A ( x .ih y ) = 0 } $=
      ( chil wss cort cfv cv csp co cc0 wceq wral crab chssii ocval ax-mp ) CEF
      CGHAIBIJKLMBCNAEOMCDPABCQR $.
      $( [8-Aug-2004] $)
  $}

  ${
    shuni.1 $e |- ( ph -> H e. SH ) $.
    shuni.2 $e |- ( ph -> K e. SH ) $.
    shuni.3 $e |- ( ph -> ( H i^i K ) = 0H ) $.
    shuni.4 $e |- ( ph -> A e. H ) $.
    shuni.5 $e |- ( ph -> B e. K ) $.
    shuni.6 $e |- ( ph -> C e. H ) $.
    shuni.7 $e |- ( ph -> D e. K ) $.
    shuni.8 $e |- ( ph -> ( A +h B ) = ( C +h D ) ) $.
    $( Two subspaces with trivial intersection have a unique decomposition of
       the elements of the subspace sum.  (Contributed by Mario Carneiro,
       15-May-2014.) $)
    shuni $p |- ( ph -> ( A = C /\ B = D ) ) $=
      ( wceq co wcel chil syl2anc cmv c0v c0h cin csh shsubcl syl3anc hvaddsub4
      wb shel syl22anc mpbid eqeltrd elin sylanbrc eleqtrd elch0 sylib hvsubeq0
      cva eqtr3d eqcomd jca ) ABDPZCEPABDUAQZUBPZVDAVEUCRVFAVEFGUDZUCAVEFRZVEGR
      VEVGRAFUERZBFRZDFRZVHHKMBDFUFUGAVEECUAQZGABCUTQDEUTQPZVEVLPZOABSRZCSRZDSR
      ZESRZVMVNUIAVIVJVOHKBFUJTZAGUERZCGRZVPILCGUJTZAVIVKVQHMDFUJTZAVTEGRZVRINE
      GUJTZBCDEUHUKULZAVTWDWAVLGRINLECGUFUGUMVEFGUNUOJUPVEUQURZAVOVQVFVDUIVSWCB
      DUSTULAECAVLUBPZECPZAVEVLUBWFWGVAAVRVPWHWIUIWEWBECUSTULVBVC $.
      $( [15-May-2014] $)
  $}

  ${
    chocuni.1 $e |- H e. CH $.
    $( Lemma for uniqueness part of Projection Theorem.  Theorem 3.7(i) of
       [Beran] p. 102 (uniqueness part).  (The proof was shortened by Mario
       Carneiro, 15-May-2014.) $)
    chocunii $p |- ( ( ( A e. H /\ B e. ( _|_ ` H ) ) /\
                    ( C e. H /\ D e. ( _|_ ` H ) ) ) ->
            ( ( R = ( A +h B ) /\ R = ( C +h D ) ) -> ( A = C /\ B = D ) ) ) $=
      ( wcel cort cfv wa cva co wceq csh chshii a1i shocsh ax-mp cin ocin eqtr2
      c0h simplll simpllr simplrl simplrr adantl shuni ex ) AFHZBFIJZHZKZCFHZDU
      LHZKZKZEABLMZNECDLMZNKZACNBDNKURVAKZABCDFULFOHZVBFGPZQULOHZVBVCVEVDFRSQFU
      LTUCNZVBVCVFVDFUASQUKUMUQVAUDUKUMUQVAUEUNUOUPVAUFUNUOUPVAUGVAUSUTNUREUSUT
      UBUHUIUJ $.
      $( [15-May-2014] $) $( [23-Oct-1999] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w x y z C $.
    $( Projection Theorem, uniqueness part.  Any two disjoint subspaces yield a
       unique decomposition of vectors into each subspace.  (Contributed by
       Mario Carneiro, 15-May-2014.) $)
    pjthmo $p |- ( ( A e. SH /\ B e. SH /\ ( A i^i B ) = 0H ) ->
                   E* x ( x e. A /\ E. y e. B C = ( x +h y ) ) ) $=
      ( vz vw csh wcel cin c0h wceq cv cva co wrex wa wal syl5bir eqeq2d w3a wi
      wmo reeanv simpll1 simpll2 simpll3 simplrl simprll simplrr simprlr eqtr3d
      simprrl simprrr shuni simpld exp32 rexlimdvv expimpd alrimivv eleq1 oveq1
      an4 rexbidv oveq2 cbvrexv syl6bb anbi12d mo4 sylibr ) CHIZDHIZCDJKLZUAZAM
      ZCIZEVOBMZNOZLZBDPZQZFMZCIZEWBGMZNOZLZGDPZQZQZVOWBLZUBZFRARWAAUCVNWKAFWIV
      PWCQZVTWGQZQVNWJVPWCVTWGVCVNWLWMWJWMVSWFQZGDPBDPVNWLQZWJVSWFBGDDUDWOWNWJB
      GDDWOVQDIZWDDIZQZWNWJWOWRWNQZQZWJVQWDLZWTVOVQWBWDCDVKVLVMWLWSUEVKVLVMWLWS
      UFVKVLVMWLWSUGVNVPWCWSUHWOWPWQWNUIVNVPWCWSUJWOWPWQWNUKWTEVRWEWOWRVSWFUMWO
      WRVSWFUNULUOUPUQURSUSSUTWAWHAFWJVPWCVTWGVOWBCVAWJVTEWBVQNOZLZBDPWGWJVSXCB
      DWJVRXBEVOWBVQNVBTVDXCWFBGDXAXBWEEVQWDWBNVETVFVGVHVIVJ $.
      $( [14-May-2014] $)
  $}

  ${
    $d k x B $.  $d k x F $.  $d k x ph $.  $d x A $.
    occl.1 $e |- ( ph -> A C_ ~H ) $.
    occl.2 $e |- ( ph -> F e. Cauchy ) $.
    occl.3 $e |- ( ph -> F : NN --> ( _|_ ` A ) ) $.
    occl.4 $e |- ( ph -> B e. A ) $.
    $( Lemma for ~ occl . $)
    occllem $p |- ( ph -> ( ( ~~>v ` F ) .ih B ) = 0 ) $=
      ( vx chli cfv csp co cc0 cn wcel a1i chil ax-mp wceq vk csn cxp cabs cmin
      ccom copn cvv cc cha eqid cnmet methausi ovex 0cn cv cmpt clm cno cmv cme
      ctop hilmet opntop wbr cdm ccau wrex ax-hcompl wfn wf hlimf ffn fnbr mpan
      rexlimivw 3syl wfun ffun funfvbrb mp2b sylib cmap cres cva csm hhims hhlm
      wb cop resss eqsstri ssbri syl hilmetba uniopn2 cnmptid sseldd cnmptc ctx
      cuni ccn cnv hhnv hhip ipcn cnmpt12f fvex hlimveci lmcn wral cort ffvelrn
      sylan wss ocel adantr mpbid simpld oveq1 fvmpt simprd oveq2 eqeq1d rcla4v
      wa sylc eqtrd funmpt ocss fss syl2anc simpr fvco3 syl3anc fvconst2 adantl
      elexi 3eqtr4d c1 ralrimiva fnmpt mprg fnfco fconst eqfnfv2 sylancl mpbird
      3brtr3d cz 1z cnmetba nnuz lmconst lmmo ) ADJKZCLMZNONUBZUCZUDUEUFZUGKZUH
      UIUVAUJPAUUTUVAUUTUUTUKZULZUVAUKZUMQUUQUHPAUUPCLUNZQNUIPZAUOQZAIRIUPZCLMZ
      UQZDUFZUUPUVJKZUUSUUQUVAURKZAUUPDUVJUSUTUFZUGKZUVARUVOVBPZAUVNVAPZUVPUVNU
      VNUKZVCZUVNUVOUVOUKZVDSQZUVAVBPZAUUTVAPZUWBUVCUUTUVAUVDVDSQZADUUPJVEZDUUP
      UVOURKZVEADJVFZPZUWEADVGPDUVHJVEZIRVHUWHFIDVIUWIUWHIRJUWGVJZUWIUWHUWGRJVK
      ZUWJVLUWGRJVMSUWGDUVHJVNVOVPVQUWKJVRUWHUWEWIVLUWGRJVSDJVTWAWBZJUWFDUUPJUW
      FROWCMZWDUWFUVNWEWFWJUSWJZUVOUWNUKZUVNUWNUWOUVRWGZUVTWHUWFUWMWKWLWMWNAIUV
      HCLUVOUVOUVOUVARRUVQRUVOXATUVSUVNUVORUVNUVRWOUVTWPSZUWAUWQUWAUWAAIUVORUWQ
      UWAWQAICUVOUVORRUWQUWAUWQUWAABRCEHWRWSUWDLUVOUVOWTMUVAXBMPZAUWNXCPUWRUWNU
      WOXDUVNUUTLUWNUVOUVAUWNUWOXEUWPUVBUVTUVDXFSQXGAUWEUUPRPZUWLUUPDDJXHXIWNZX
      JAUVKUUSTZUAUPZUVKKZUXBUUSKZTZUAOXKZAUXEUAOAUXBOPZYFZUXBDKZUVJKZNUXCUXDUX
      HUXJUXICLMZNUXHUXIRPZUXJUXKTUXHUXLUXIUVHLMZNTZIBXKZUXHUXIBXLKZPZUXLUXOYFZ
      AOUXPDVKZUXGUXQGOUXPUXBDXMXNAUXQUXRWIZUXGABRXOZUXTEIUXIBXPWNXQXRZXSIUXIUV
      IUXKRUVJUVHUXICLXTUVJUKZUXICLUNYAWNUXHCBPZUXOUXKNTZAUYDUXGHXQUXHUXLUXOUYB
      YBUXNUYEICBUVHCTUXMUXKNUVHCUXILYCYDYEYGYHUXHUVJVRZORDVKZUXGUXCUXJTUYFUXHI
      RUVIYIQAUYGUXGAUXSUXPRXOZUYGGAUYAUYHEBYJWNOUXPRDYKYLZXQAUXGYMORUXBUVJDYNY
      OUXGUXDNTAONUXBNUIUOYRZYPYQYSUUAAUVKOVJZUUSOVJZUXAUXFWIAUVJRVJZUYGUYKUYMA
      UVIUHPZUYMIRIRUVIUVJUHUYCUUBUYNUVHRPUVHCLUNQUUCQUYIROUVJDUUDYLOUURUUSVKUY
      LONUYJUUEOUURUUSVMSUAOUVKUUSUUFUUGUUHAUWSUVLUUQTUWTIUUPUVIUUQRUVJUVHUUPCL
      XTUYCUVEYAWNUUIAUWBUVFYTUUJPZUUSNUVMVEUWDUVGUYOAUUKQNUVAYTUIOUWCUIUVAXATU
      VCUUTUVAUIUUTUVBUULUVDWPSUUMUUNYOUUO $.
      $( [14-May-2014] $) $( [7-Aug-2000] $)
  $}

  ${
    $d f x A $.
    $( Closure of complement of Hilbert subset.  Part of Remark 3.12 of [Beran]
       p. 107.  (The proof was shortened by Mario Carneiro, 14-May-2014.) $)
    occl $p |- ( A C_ ~H -> ( _|_ ` A ) e. CH ) $=
      ( vf vx chil wss cort cfv wcel cv wf chli wbr wrex ccau wral wa syl hlimf
      wb ralrimiva csh cn cch ocsh csp cc0 wceq ocel ad2antrr cdm ax-hcompl vex
      wi co breldm rexlimivw ad2antlr ffvelrni simplll simpllr simplr mpbir2and
      simpr occllem wfun funfvbrb sylib breq2 rcla4ev syl2anc ex isch3 sylanbrc
      ffun mp2b ) ADEZAFGZUAHUBVQBIZJZVRCIZKLZCVQMZUMZBNOVQUCHAUDVPWCBNVPVRNHZP
      ZVSWBWEVSPZVRKGZVQHZVRWGKLZWBWFWHWGDHZWGVTUEUNUFUGZCAOZVPWHWJWLPSWDVSCWGA
      UHUIWFVRKUJZHZWJWDWNVPVSWDWACDMWNCVRUKWAWNCDVRVTKBULUOUPQUQZWMDVRKRURQWFW
      KCAWFVTAHZPAVTVRVPWDVSWPUSVPWDVSWPUTWEVSWPVAWFWPVCVDTVBWFWNWIWOWMDKJKVEWN
      WISRWMDKVNVRKVFVOVGWAWICWGVQVTWGVRKVHVIVJVKTCBVQVLVM $.
      $( [14-May-2014] $) $( [8-Aug-2000] $)
  $}

  $( Closure of complement of Hilbert subspace.  Part of Remark 3.12 of [Beran]
     p. 107. $)
  shoccl $p |- ( A e. SH -> ( _|_ ` A ) e. CH ) $=
    ( csh wcel chil wss cort cfv cch shss occl syl ) ABCADEAFGHCAIAJK $.
    $( [13-Oct-1999] $)

  $( Closure of complement of Hilbert subspace.  Part of Remark 3.12 of [Beran]
     p. 107. $)
  choccl $p |- ( A e. CH -> ( _|_ ` A ) e. CH ) $=
    ( cch wcel csh cort cfv chsh shoccl syl ) ABCADCAEFBCAGAHI $.
    $( [22-Jul-2001] $)

  ${
    choccl.1 $e |- A e. CH $.
    $( Closure of ` CH ` orthocomplement. $)
    choccli $p |- ( _|_ ` A ) e. CH $=
      ( cch wcel cort cfv choccl ax-mp ) ACDAEFCDBAGH $.
      $( [29-Jul-1999] $)
  $}

