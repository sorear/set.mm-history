$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Atoms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Define the set of atoms in a Hilbert lattice.  An atom is a nonzero
       element of a lattice such that anything less than it is zero, i.e. it is
       a smallest nonzero element of the lattice.  Definition of atom in
       [Kalmbach] p. 15.  See ~ ela and ~ elat2 for membership relations. $)
    df-at $a |- HAtoms = { x e. CH | 0H <oH x } $.
  $}

  ${
    $d x y A $.  $d x B $.
    $( Atoms in a Hilbert lattice are the elements that cover the zero
       subspace.  Definition of atom in [Kalmbach] p. 15. $)
    ela $p |- ( A e. HAtoms <-> ( A e. CH /\ 0H <oH A ) ) $=
      ( vx c0h cv ccv wbr cch cat breq2 df-at elrab2 ) CBDZEFCAEFBAGHLACEIBJK
      $.
      $( [9-Jun-2004] $)

    $( Expanded membership relation for the set of atoms, i.e. the predicate
       "is an atom (of the Hilbert lattice)."  An atom is a nonzero element of
       a lattice such that anything less than it is zero, i.e. it is a smallest
       nonzero element of the lattice. $)
    elat2 $p |- ( A e. HAtoms <-> ( A e. CH /\ ( A =/= 0H /\
                 A. x e. CH ( x C_ A -> ( x = A \/ x = 0H ) ) ) ) ) $=
      ( cat wcel cch c0h ccv wbr wa wne cv wceq wo wi wral wpss wb ch0pss bitri
      wss ela h0elch cvbr2 mpan imbi1d imbi2d impexp bi2.04 neor imbi2i 3bitr4g
      orcom ralbiia a1i anbi12d bitr2d pm5.32i bitr4i ) BCDBEDZFBGHZIUSBFJZAKZB
      TZVBBLZVBFLZMZNZAEOZIZIBUAUSVIUTUSUTFBPZFVBPZVCIVDNZAEOZIZVIFEDUSUTVNQUBA
      FBUCUDUSVJVAVMVHBRVMVHQUSVLVGAEVBEDZVCVKVDNZNZVCVBFJZVDNZNVLVGVOVPVSVCVOV
      KVRVDVBRUEUFVLVKVCVDNNVQVKVCVDUGVKVCVDUHSVFVSVCVFVEVDMVSVDVEULVDVBFUISUJU
      KUMUNUOUPUQUR $.
      $( [18-Jun-2006] $) $( [9-Jun-2004] $)

    $( A Hilbert lattice element is an atom iff it covers the zero subspace. $)
    elatcv0 $p |- ( A e. CH -> ( A e. HAtoms <-> 0H <oH A ) ) $=
      ( cat wcel cch c0h ccv wbr ela baib ) ABCADCEAFGAHI $.
      $( [14-Feb-2005] $) $( [26-Jun-2004] $)

    $( An atom covers the zero subspace. $)
    atcv0 $p |- ( A e. HAtoms -> 0H <oH A ) $=
      ( cat wcel cch c0h ccv wbr ela simprbi ) ABCADCEAFGAHI $.
      $( [26-Jun-2004] $)

    $( Atoms are a subset of the Hilbert lattice. $)
    atssch $p |- HAtoms C_ CH $=
      ( vx cat c0h cv ccv wbr cch crab df-at ssrab2 eqsstri ) BCADEFZAGHGAILAGJ
      K $.
      $( [14-Aug-2002] $)

    $( An atom is a Hilbert lattice element. $)
    atelch $p |- ( A e. HAtoms -> A e. CH ) $=
      ( cat cch atssch sseli ) BCADE $.
      $( [22-Jun-2004] $)

    $( An atom is not the Hilbert lattice zero. $)
    atne0 $p |- ( A e. HAtoms -> A =/= 0H ) $=
      ( vx cat wcel cch c0h wne cv wss wceq wo wi wral wa elat2 simprl sylbi )
      ACDAEDZAFGZBHZAITAJTFJKLBEMZNNSBAORSUAPQ $.
      $( [27-Jun-2006] $) $( [13-Aug-2002] $)

    $( A lattice element smaller than an atom is either the atom or zero. $)
    atss $p |- ( ( A e. CH /\ B e. HAtoms ) ->
               ( A C_ B -> ( A = B \/ A = 0H ) ) ) $=
      ( vx cat wcel cch c0h wne cv wceq wo wi wral wa elat2 sseq1 eqeq1 orbi12d
      wss adantld imbi12d rcla4v imp sylan2b ) BDEAFEZBFEZBGHZCIZBSZUHBJZUHGJZK
      ZLZCFMZNZNZABSZABJZAGJZKZLZCBOUEUPVAUEUOVAUFUEUNVAUGUMVACAFUHAJZUIUQULUTU
      HABPVBUJURUKUSUHABQUHAGQRUAUBTTUCUD $.
      $( [25-Jun-2004] $)
  $}

  $( Two atoms in a subset relationship are equal. $)
  atsseq $p |- ( ( A e. HAtoms /\ B e. HAtoms ) -> ( A C_ B <-> A = B ) ) $=
    ( cat wcel wa wss wceq c0h wne atne0 ad2antrr wo cch wi atelch atss imp ord
    sylan necon1ad mpd ex eqimss impbid1 ) ACDZBCDZEZABFZABGZUGUHUIUGUHEZAHIZUI
    UEUKUFUHAJKUJUIAHUJUIAHGZUGUHUIULLZUEAMDUFUHUMNAOABPSQRTUAUBABUCUD $.
    $( [26-Jun-2004] $)

  $( A Hilbert lattice element covered by an atom must be the zero subspace. $)
  atcveq0 $p |- ( ( A e. CH /\ B e. HAtoms ) -> ( A <oH B <-> A = 0H ) ) $=
    ( cch wcel cat wa ccv wbr c0h wceq wpss wi atelch cvpss sylan2 ch0le adantr
    wss jctild atcv0 h0elch cvnbtwn3 mp3an1 sylan ancoms syld syl5ibrcom adantl
    mpd breq1 impbid ) ACDZBEDZFZABGHZAIJZUNUOIARZABKZFZUPUNUOURUQUMULBCDZUOURL
    BMZABNOULUQUMAPQSUMULUSUPLZUMULFIBGHZVBUMVCULBTZQUMUTULVCVBLZVAICDUTULVEUAI
    BAUBUCUDUIUEUFUMUPUOLULUMUOUPVCVDAIBGUJUGUHUK $.
    $( [11-Jun-2004] $)

  ${
    $d x A $.
    $( A 1-dimensional subspace is an atom. $)
    h1da $p |- ( ( A e. ~H /\ A =/= 0h ) ->
                ( _|_ ` ( _|_ ` { A } ) ) e. HAtoms ) $=
      ( vx chil wcel c0v wne wa csn cort cfv cch c0h cv wss wceq wo wi wral cat
      adantr snssi occl choccl h1dn0 h1datom expcom ralrimiv jca elat2 sylanbrc
      3syl ) ACDZAEFZGZAHZIJZIJZKDZUQLFZBMZUQNUTUQOUTLOPQZBKRZGUQSDULURUMULUOCN
      UPKDURACUAUOUBUPUCUKTUNUSVBAUDULVBUMULVABKUTKDULVAUTAUEUFUGTUHBUQUIUJ $.
      $( [2-Jun-2007] $) $( [22-Jul-2001] $)
  $}

  $( The span of the singleton of a vector is an atom. $)
  spansna $p |- ( ( A e. ~H /\ A =/= 0h ) -> ( span ` { A } ) e. HAtoms ) $=
    ( chil wcel c0v wne csn cspn cfv cort cat wceq spansn adantr h1da eqeltrd
    wa ) ABCZADEZPAFZGHZSIHIHZJQTUAKRALMANO $.
    $( [20-Jun-2006] $) $( [18-Dec-2004] $)

  $( A 1-dimensional subspace is less than or equal to any subspace containing
     its generating vector. $)
  sh1dle $p |- ( ( A e. SH /\ B e. A ) -> ( _|_ ` ( _|_ ` { B } ) ) C_ A ) $=
    ( csh wcel wa csn cort cfv cspn chil wceq shel spansn syl spansnss eqsstr3d
    ) ACDBADEZBFZGHGHZRIHZAQBJDTSKBALBMNABOP $.
    $( [24-Nov-2004] $)

  $( A 1-dimensional subspace is less than or equal to any member of ` CH `
     containing its generating vector. $)
  ch1dle $p |- ( ( A e. CH /\ B e. A ) -> ( _|_ ` ( _|_ ` { B } ) ) C_ A ) $=
    ( cch wcel csh csn cort cfv wss chsh sh1dle sylan ) ACDAEDBADBFGHGHAIAJABKL
    $.
    $( [30-May-2004] $)

  ${
    $d x y z A $.
    $( The 1-dimensional subspaces of Hilbert space are its atoms.  Part of
       Remark 10.3.5 of [BeltramettiCassinelli] p. 107. $)
    atom1d $p |- ( A e. HAtoms <->
                E. x e. ~H ( x =/= 0h /\ A = ( span ` { x } ) ) ) $=
      ( vy cat wcel cv wne cort cfv wceq wa chil wrex cch c0h wss wo wi adantrr
      ax-17 c0v csn cspn wral elat2 chne0 hbre1 hbim simprlr h1dn0 sylan anasss
      chel ch1dle snssi occl 3syl choccl sseq1 orbi12d imbi12d rcla4v mpid impr
      wn eqeq1 adantrlr ord nne syl6ibr mt4d eqcomd ra4e syl12anc exp44 rexlimd
      sylbid imp32 sylbi eleq1 syl5ibr exp3acom3r rexlimiv impbii spansn eqeq2d
      h1da imp3a anbi2d rexbiia bitr4i ) BDEZAFZUAGZBWMUBZHIZHIZJZKZALMZWNBWOUC
      IZJZKZALMWLWTWLBNEZBOGZCFZBPZXFBJZXFOJZQZRZCNUDZKKWTCBUEXDXEXLWTXDXEWNABM
      XLWTRZABUFXDWNXMABXDATXLWTAXLATWSALUGUHXDWMBEZWNXLWTXDXNWNKZXLKKZWMLEZWNW
      RWTXDXOXQXLXDXNXQWNWMBUMZSSXDXNWNXLUIXPWQBXPWQOGZWQBJZXDXOXSXLXDXNWNXSXDX
      NKZXQWNXSXRWMUJUKULSXPXTVEWQOJZXSVEXPXTYBXDXNXLXTYBQZWNXDXNXLYCYAXLWQBPZY
      CBWMUNYAWPNEZWQNEXLYDYCRZRYAXQWOLPYEXRWMLUOWOUPUQWPURXKYFCWQNXFWQJZXGYDXJ
      YCXFWQBUSYGXHXTXIYBXFWQBVFXFWQOVFUTVAVBUQVCVDVGVHWQOVIVJVKVLWSALVMVNVOVPV
      QVRVSWSWLALXQWNWRWLWRXQWNWLXQWNKWLWRWQDEWMWGBWQDVTWAWBWHWCWDXCWSALXQXBWRW
      NXQXAWQBWMWEWFWIWJWK $.
      $( [25-Jun-2006] $) $( [4-Jun-2004] $)
  $}


