$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Relate Hilbert space to ZFC pre-Hilbert and Hilbert spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f j k t u v w x y z D $.
    hhcms.1 $e |- U = <. <. +h , .h >. , normh >. $.
    hhcms.2 $e |- D = ( IndMet ` U ) $.
    $( The Hilbert space induced metric determines a complete metric space.
       (The proof was shortened by Mario Carneiro, 14-May-2014.) $)
    hhcms $p |- D e. CMet $=
      ( vf vx copn cfv chil hhmetba eqid hhmet cv wcel cn wa chli wbr ccau vex
      cca wf wrex clm cdm cmap cin hhcau eleq2i elin ax-hilex nnex elmap anbi2i
      co 3bitri ax-hcompl sylbir cres breqi brres bitri breldm adantr rexlimivw
      hhlm sylbi syl iscms3i ) AEAGHZIABCDJVJKZABCDLEMZAUAHZNZOIVLUBZPZVLFMZQRZ
      FIUCZVLVJUDHZUENZVPVLSNZVSWBVLVMIOUFUOZUGZNVNVLWCNZPVPSWDVLABCDUHUIVLVMWC
      UJWEVOVNIOVLUKULUMUNUPFVLUQURVRWAFIVRVLVQVTRZWEPZWAVRVLVQVTWCUSZRWGVLVQQW
      HABVJCDVKVFUTVLVQVTWCFTVAVBWFWAWEVLVQVTETVCVDVGVEVHVI $.
      $( [14-May-2014] $) $( [10-Apr-2008] $)
  $}

  ${
    hhhl.1 $e |- U = <. <. +h , .h >. , normh >. $.
    $( The Hilbert space structure is a complex Hilbert space. $)
    hhhl $p |- U e. CHil $=
      ( chl wcel cbn cphl cnv cims cfv hhnv eqid hhcms isbn mpbir2an hhph ishl
      cms ) ACDAEDZAFDRAGDAHIZQDABJSABSKZLSATMNABOAPN $.
      $( [10-Apr-2008] $)
  $}

  ${
    hilcms.1 $e |- D = ( normh o. -h ) $.
    $( The Hilbert space norm determines a complete metric space. $)
    hilcms $p |- D e. CMet $=
      ( cva csm cop cno eqid hhims hhcms ) ACDEFEZJGZAJKBHI $.
      $( [17-Apr-2007] $)
  $}

  ${
    $d x y z $.
    $( The Hilbert space of the Hilbert Space Explorer is a complex Hilbert
       space.  (Contributed by Steve Rodriguez, 29-Apr-2007.) $)
    hilhl $p |- <. <. +h , .h >. , normh >. e. CHil $=
      ( cva csm cop cno eqid hhhl ) ABCDCZGEF $.
      $( [29-Apr-2007] $)
  $}

