$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Vector operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The Hilbert space scalar product operation is a set. $)
  hvmulex $p |- .h e. _V $=
    ( cc chil cxp csm wf cvv wcel ax-hfvmul cnex ax-hilex xpex fex mp2an ) ABCZ
    BDENFGDFGHABIJKNBFDLM $.
    $( [17-Apr-2007] $)

  $( Closure of vector addition. $)
  hvaddcl $p |- ( ( A e. ~H /\ B e. ~H ) -> ( A +h B ) e. ~H ) $=
    ( chil cva ax-hfvadd fovcl ) ABCCCDEF $.
    $( [18-Apr-2007] $)

  $( Closure of scalar multiplication. $)
  hvmulcl $p |- ( ( A e. CC /\ B e. ~H ) -> ( A .h B ) e. ~H ) $=
    ( chil cc csm ax-hfvmul fovcl ) ABCDCEFG $.
    $( [19-Apr-2007] $)

  ${
    hvmulcl.1 $e |- A e. CC $.
    hvmulcl.2 $e |- B e. ~H $.
    $( Closure inference for scalar multiplication. $)
    hvmulcli $p |- ( A .h B ) e. ~H $=
      ( cc wcel chil csm co hvmulcl mp2an ) AEFBGFABHIGFCDABJK $.
      $( [1-Aug-1999] $)
  $}

  ${
    $d x y $.
    $( Mapping domain and codomain of vector subtraction. $)
    hvsubopr $p |- -h : ( ~H X. ~H ) --> ~H $=
      ( vx vy cv c1 cneg csm co cva chil wcel wral cxp cmv wf cc ax-1cn hvmulcl
      negcli mpan hvaddcl sylan2 rgen2a df-hvsub fmpt2 mpbi ) ACZDEZBCZFGZHGZIJ
      ZBIKAIKIILIMNUKABIUHIJZUFIJUIIJZUKUGOJULUMDPRUGUHQSUFUITUAUBABIIUJIMABUCU
      DUE $.
      $( [6-Sep-2007] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( Value of vector subtraction.  (Revised by Mario Carneiro,
       23-Dec-2013.) $)
    hvsubval $p |- ( ( A e. ~H /\ B e. ~H ) ->
                    ( A -h B ) = ( A +h ( -u 1 .h B ) ) ) $=
      ( vx vy chil cv c1 cneg csm cva cmv oveq1 wceq oveq2 oveq2d df-hvsub ovex
      co ovmpt2 ) CDABEECFZGHZDFZIRZJRAUABIRZJRKAUCJRTAUCJLUBBMUCUDAJUBBUAINOCD
      PAUDJQS $.
      $( [23-Dec-2013] $) $( [5-Sep-1999] $)
  $}

  $( Closure of vector subtraction. $)
  hvsubcl $p |- ( ( A e. ~H /\ B e. ~H ) -> ( A -h B ) e. ~H ) $=
    ( chil wcel wa cmv co c1 cneg csm hvsubval cc ax-1cn negcli hvmulcl hvaddcl
    cva mpan sylan2 eqeltrd ) ACDZBCDZEABFGAHIZBJGZQGZCABKUBUAUDCDZUECDUCLDUBUF
    HMNUCBORAUDPST $.
    $( [17-Aug-1999] $)

  ${
    hvaddcl.1 $e |- A e. ~H $.
    hvaddcl.2 $e |- B e. ~H $.
    $( Closure of vector addition. $)
    hvaddcli $p |- ( A +h B ) e. ~H $=
      ( chil wcel cva co hvaddcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [1-Aug-1999] $)

    $( Commutation of vector addition. $)
    hvcomi $p |- ( A +h B ) = ( B +h A ) $=
      ( chil wcel cva co wceq ax-hvcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [3-Sep-1999] $)

    $( Value of vector subtraction definition. $)
    hvsubvali $p |- ( A -h B ) = ( A +h ( -u 1 .h B ) ) $=
      ( chil wcel cmv co c1 cneg csm cva wceq hvsubval mp2an ) AEFBEFABGHAIJBKH
      LHMCDABNO $.
      $( [3-Sep-1999] $)

    $( Closure of vector subtraction. $)
    hvsubcli $p |- ( A -h B ) e. ~H $=
      ( chil wcel cmv co hvsubcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [2-Aug-1999] $)
  $}

  $( Addition with the zero vector. $)
  hvaddid2 $p |- ( A e. ~H -> ( 0h +h A ) = A ) $=
    ( chil wcel c0v cva co wceq ax-hv0cl ax-hvcom mpan2 ax-hvaddid eqtr3d ) ABC
    ZADEFZDAEFZAMDBCNOGHADIJAKL $.
    $( [18-Oct-1999] $)

  $( Scalar multiplication with the zero vector. $)
  hvmul0 $p |- ( A e. CC -> ( A .h 0h ) = 0h ) $=
    ( cc wcel c0v cc0 csm cmul mul01 oveq1d chil wceq ax-hv0cl ax-hvmul0 syl6eq
    co ax-mp 0cn ax-hvmulass mp3an23 eqtr3d oveq2i syl6req ) ABCZDAEDFOZFOZADFO
    UCAEGOZDFOZDUEUCUGUDDUCUFEDFAHIDJCZUDDKLDMPZNUCEBCUHUGUEKQLAEDRSTUDDAFUIUAU
    B $.
    $( [30-May-1999] $)

  $( If a scalar product is zero, one of its factors must be zero. $)
  hvmul0or $p |- ( ( A e. CC /\ B e. ~H ) ->
                  ( ( A .h B ) = 0h <-> ( A = 0 \/ B = 0h ) ) ) $=
    ( cc wcel wa csm co c0v wceq c1 oveq2 ad2antlr adantlr 3eqtr3d hvmul0 ex wi
    cc0 eqeq1d syl5ibrcom chil wo wn df-ne cdiv cmul recid2 oveq1d reccl simpll
    wne simplr ax-hvmulass syl3anc ax-hvmulid syl syl5bir orrd ax-hvmul0 adantl
    oveq1 adantr jaod impbid ) ACDZBUADZEZABFGZHIZARIZBHIZUBZVGVIVLVGVIEZVJVKVJ
    UCARUKZVMVKARUDVMVNVKVMVNEJAUEGZVHFGZVOHFGZBHVIVPVQIVGVNVHHVOFKLVGVNVPBIVIV
    GVNEZVOAUFGZBFGZJBFGZVPBVEVNVTWAIVFVEVNEZVSJBFAUGUHMVRVOCDZVEVFVTVPIVEVNWCV
    FAUIZMVEVFVNUJVEVFVNULVOABUMUNVFWABIVEVNBUOLNMVGVNVQHIZVIVEVNWEVFWBWCWEWDVO
    OUPMMNPUQURPVGVJVIVKVFVJVIQVEVFVIVJRBFGZHIBUSVJVHWFHARBFVASTUTVEVKVIQVFVEVI
    VKAHFGZHIAOVKVHWGHBHAFKSTVBVCVD $.
    $( [19-May-2005] $)

  $( Subtraction of a vector from itself. $)
  hvsubid $p |- ( A e. ~H -> ( A -h A ) = 0h ) $=
    ( chil wcel cmv co cc0 csm c0v cneg caddc cva ax-hvmulid oveq1d wceq ax-1cn
    c1 cc negcli ax-hvdistr2 mp3an12 hvsubval anidms negidi oveq1i syl6eq eqtrd
    3eqtr4rd ax-hvmul0 ) ABCZAADEZFAGEZHUIUJPPIZJEZAGEZUKUIPAGEZULAGEZKEZAUPKEZ
    UNUJUIUOAUPKALMPQCULQCUIUNUQNOPORPULASTUIUJURNAAUAUBUGUMFAGPOUCUDUEAUHUF $.
    $( [30-May-1999] $)

  $( Addition of negative of a vector to itself. $)
  hvnegid $p |- ( A e. ~H -> ( A +h ( -u 1 .h A ) ) = 0h ) $=
    ( chil wcel cmv co c1 cneg csm cva c0v wceq hvsubval anidms hvsubid eqtr3d
    ) ABCZAADEZAFGAHEIEZJPQRKAALMANO $.
    $( [4-Jun-2004] $)

  $( Two ways to express the negative of a vector. $)
  hv2neg $p |- ( A e. ~H -> ( 0h -h A ) = ( -u 1 .h A ) ) $=
    ( chil wcel c0v cmv co c1 cneg csm cva wceq ax-hv0cl hvsubval ax-1cn negcli
    mpan cc hvmulcl hvaddid2 syl eqtrd ) ABCZDAEFZDGHZAIFZJFZUEDBCUBUCUFKLDAMPU
    BUEBCZUFUEKUDQCUBUGGNOUDARPUESTUA $.
    $( [23-May-2005] $)

  ${
    hvaddid2.1 $e |- A e. ~H $.
    $( Addition with the zero vector. $)
    hvaddid2i $p |- ( 0h +h A ) = A $=
      ( chil wcel c0v cva co wceq hvaddid2 ax-mp ) ACDEAFGAHBAIJ $.
      $( [18-Aug-1999] $)

    $( Addition of negative of a vector to itself. $)
    hvnegidi $p |- ( A +h ( -u 1 .h A ) ) = 0h $=
      ( chil wcel c1 cneg csm co cva c0v wceq hvnegid ax-mp ) ACDAEFAGHIHJKBALM
      $.
      $( [18-Aug-1999] $)

    $( Two ways to express the negative of a vector. $)
    hv2negi $p |- ( 0h -h A ) = ( -u 1 .h A ) $=
      ( chil wcel c0v cmv co c1 cneg csm wceq hv2neg ax-mp ) ACDEAFGHIAJGKBALM
      $.
      $( [31-Oct-1999] $)
  $}

  $( Convert minus one times a scalar product to the negative of the scalar. $)
  hvm1neg $p |- ( ( A e. CC /\ B e. ~H ) ->
                 ( -u 1 .h ( A .h B ) ) = ( -u A .h B ) ) $=
    ( cc wcel chil wa c1 cneg cmul co csm wceq ax-1cn negcli ax-hvmulass mp3an1
    mulm1 adantr oveq1d eqtr3d ) ACDZBEDZFZGHZAIJZBKJZUDABKJKJZAHZBKJUDCDUAUBUF
    UGLGMNUDABOPUCUEUHBKUAUEUHLUBAQRST $.
    $( [4-Jun-2004] $)

  $( Value of vector addition in terms of vector subtraction. $)
  hvaddsubval $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( A +h B ) = ( A -h ( -u 1 .h B ) ) ) $=
    ( chil wcel wa c1 cneg csm cmv cva wceq ax-1cn negcli hvmulcl mpan hvsubval
    co cc sylan2 hvm1neg negnegi oveq1i syl6eq ax-hvmulid adantl oveq2d eqtr2d
    eqtrd ) ACDZBCDZEZAFGZBHQZIQZAULUMHQZJQZABJQUJUIUMCDZUNUPKULRDZUJUQFLMZULBN
    OAUMPSUKUOBAJUJUOBKUIUJUOFBHQZBUJUOULGZBHQZUTURUJUOVBKUSULBTOVAFBHFLUAUBUCB
    UDUHUEUFUG $.
    $( [10-Jun-2006] $)

  $( Commutative/associative law. $)
  hvadd32 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
               ( ( A +h B ) +h C ) = ( ( A +h C ) +h B ) ) $=
    ( chil wcel w3a cva co wceq ax-hvcom oveq2d 3adant1 ax-hvass 3com23 3eqtr4d
    wa ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSPTUBAGBCJKLABC
    MQSRUDUCIACBMNO $.
    $( [16-Oct-1999] $)

  $( Commutative/associative law. $)
  hvadd12 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
               ( A +h ( B +h C ) ) = ( B +h ( A +h C ) ) ) $=
    ( chil wcel w3a cva co wceq ax-hvcom oveq1d 3adant3 ax-hvass 3com12 3eqtr3d
    wa ) ADEZBDEZCDEZFABGHZCGHZBAGHZCGHZABCGHGHBACGHGHZQRUAUCISQRPTUBCGABJKLABC
    MRQSUCUDIBACMNO $.
    $( [19-Oct-1999] $)

  $( Hilbert vector space addition law. $)
  hvadd4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
               ( ( A +h B ) +h ( C +h D ) ) = ( ( A +h C ) +h ( B +h D ) ) ) $=
    ( chil wcel wa cva co wceq w3a hvadd32 3expa adantrr hvaddcl ax-hvass 3expb
    oveq1d sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZBHIZDHIZ
    UHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLRMNUDUHEFZUGUJUNJZAB
    OUQUEUFURUHCDPQSUBUEUCUFUMUOJZUBUEGUKEFZUCUFGUSACOUTUCUFUSUKBDPQSTUA $.
    $( [16-Oct-1999] $)

  $( Hilbert vector space addition/subtraction law. $)
  hvsub4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
               ( ( A +h B ) -h ( C +h D ) ) = ( ( A -h C ) +h ( B -h D ) ) ) $=
    ( chil wcel wa cva co c1 cneg csm cmv wceq cc hvmulcl mpan hvaddcl hvsubval
    anim2i ax-1cn negcli ax-hvdistr1 mp3an1 adantl oveq2d anim12i hvadd4 eqtr4d
    an4s syl syl2an ad2ant2r ad2ant2l oveq12d 3eqtr4d ) AEFZBEFZGZCEFZDEFZGZGZA
    BHIZJKZCDHIZLIZHIZAVECLIZHIZBVEDLIZHIZHIZVDVFMIZACMIZBDMIZHIVCVHVDVIVKHIZHI
    ZVMVCVGVQVDHVBVGVQNZUSVEOFZUTVAVSJUAUBZVECDUCUDUEUFVCUQVIEFZGZURVKEFZGZGZVM
    VRNUQUTURVAWFUQUTGWCURVAGWEUTWBUQVTUTWBWAVECPQTVAWDURVTVAWDWAVEDPQTUGUJAVIB
    VKUHUKUIUSVDEFVFEFVNVHNVBABRCDRVDVFSULVCVOVJVPVLHUQUTVOVJNURVAACSUMURVAVPVL
    NUQUTBDSUNUOUP $.
    $( [17-Oct-1999] $)

  $( Commutative/associative law. $)
  hvaddsub12 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
               ( A +h ( B -h C ) ) = ( B +h ( A -h C ) ) ) $=
    ( chil wcel w3a c1 cneg csm co cva cmv cc ax-1cn negcli hvmulcl wa hvsubval
    wceq oveq2d mpan hvadd12 syl3an3 3adant1 3adant2 3eqtr4d ) ADEZBDEZCDEZFABG
    HZCIJZKJZKJZBAUKKJZKJZABCLJZKJZBACLJZKJZUIUGUHUKDEZUMUOSUJMEUIUTGNOUJCPUAAB
    UKUBUCUHUIUQUMSUGUHUIQUPULAKBCRTUDUGUIUSUOSUHUGUIQURUNBKACRTUEUF $.
    $( [19-Oct-1999] $)

  $( Addition/subtraction cancellation law for vectors in Hilbert space. $)
  hvpncan $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( A +h B ) -h B ) = A ) $=
    ( chil wcel wa cva co cmv c1 cneg csm c0v wceq hvaddcl hvsubval sylancom cc
    ax-1cn negcli hvmulcl mpan ancli ax-hvass 3expb sylan2 eqtrd hvnegid oveq2d
    adantl ax-hvaddid adantr 3eqtrd ) ACDZBCDZEZABFGZBHGZABIJZBKGZFGZFGZALFGZAU
    OUQUPUSFGZVAUMUNUPCDUQVCMABNUPBOPUNUMUNUSCDZEVCVAMZUNVDURQDUNVDIRSURBTUAUBU
    MUNVDVEABUSUCUDUEUFUNVAVBMUMUNUTLAFBUGUHUIUMVBAMUNAUJUKUL $.
    $( [7-Jun-2004] $)

  $( Addition/subtraction cancellation law for vectors in Hilbert space. $)
  hvpncan2 $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( A +h B ) -h A ) = B ) $=
    ( chil wcel cva co cmv wceq wa ax-hvcom oveq1d hvpncan eqtr3d ancoms ) BCDZ
    ACDZABEFZAGFZBHOPIZBAEFZAGFRBSTQAGBAJKBALMN $.
    $( [7-Jun-2004] $)

  $( Associativity of sum and difference of Hilbert space vectors. $)
  hvaddsubass $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) -> ( ( A +h B ) -h C )
                     = ( A +h ( B -h C ) ) ) $=
    ( chil wcel w3a cva co c1 cneg csm cmv wceq cc ax-1cn hvmulcl mpan ax-hvass
    negcli hvsubval syl3an3 wa hvaddcl sylan 3impa 3adant1 oveq2d 3eqtr4d ) ADE
    ZBDEZCDEZFZABGHZIJZCKHZGHZABUOGHZGHZUMCLHZABCLHZGHUKUIUJUODEZUPURMUNNEUKVAI
    OSUNCPQABUORUAUIUJUKUSUPMZUIUJUBUMDEUKVBABUCUMCTUDUEULUTUQAGUJUKUTUQMUIBCTU
    FUGUH $.
    $( [27-Aug-2004] $)

  $( Subtraction and addition of equal Hilbert space vectors.. $)
  hvpncan3 $p |- ( ( A e. ~H /\ B e. ~H ) -> ( A +h ( B -h A ) ) = B ) $=
    ( chil wcel wa cva co cmv wceq hvaddsubass 3anidm13 hvpncan2 eqtr3d ) ACDZB
    CDZEABFGAHGZABAHGFGZBNOPQIABAJKABLM $.
    $( [27-Aug-2004] $)

  $( Scalar multiplication commutative law. $)
  hvmulcom $p |- ( ( A e. CC /\ B e. CC /\ C e. ~H ) ->
                 ( A .h ( B .h C ) ) = ( B .h ( A .h C ) ) ) $=
    ( cc wcel chil w3a cmul co wceq wa mulcom oveq1d 3adant3 ax-hvmulass 3com12
    csm 3eqtr3d ) ADEZBDEZCFEZGABHIZCQIZBAHIZCQIZABCQIQIBACQIQIZSTUCUEJUASTKUBU
    DCQABLMNABCOTSUAUEUFJBACOPR $.
    $( [19-May-2005] $)
  $( Hilbert vector space associative law for subtraction. $)
  hvsubass $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                   ( ( A -h B ) -h C ) = ( A -h ( B +h C ) ) ) $=
    ( chil wcel w3a c1 cneg csm co cmv cva wceq cc ax-1cn hvmulcl mpan hvsubval
    negcli 3adant1 ax-hvdistr1 mp3an1 eqtr4d oveq2d 3adant3 hvaddsubass syl3an2
    sylan oveq1d eqtrd simp1 hvaddcl syl2anc 3eqtr4d ) ADEZBDEZCDEZFZAGHZBIJZCK
    JZLJZAUSBCLJZIJZLJZABKJZCKJZAVCKJZURVAVDALURVAUTUSCIJLJZVDUPUQVAVIMZUOUPUTD
    EZUQVJUSNEZUPVKGOSZUSBPQZUTCRUHTUPUQVDVIMZUOVLUPUQVOVMUSBCUAUBTUCUDURVGAUTL
    JZCKJZVBURVFVPCKUOUPVFVPMUQABRUEUIUPUOVKUQVQVBMVNAUTCUFUGUJURUOVCDEZVHVEMUO
    UPUQUKUPUQVRUOBCULTAVCRUMUN $.
    $( [15-May-2014] $)

  $( Hilbert vector space commutative/associative law. $)
  hvsub32 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h B ) -h C ) = ( ( A -h C ) -h B ) ) $=
    ( chil wcel w3a cva co wceq ax-hvcom 3adant1 oveq2d hvsubass 3com23 3eqtr4d
    cmv ) ADEZBDEZCDEZFZABCGHZPHACBGHZPHZABPHCPHACPHBPHZTUAUBAPRSUAUBIQBCJKLABC
    MQSRUDUCIACBMNO $.
    $( [15-May-2014] $)

  ${
    hvmulcom.1 $e |- A e. CC $.
    hvmulcom.2 $e |- B e. CC $.
    hvmulcom.3 $e |- C e. ~H $.
    $( Scalar multiplication associative law. $)
    hvmulassi $p |- ( ( A x. B ) .h C ) = ( A .h ( B .h C ) ) $=
      ( cc wcel chil cmul co csm wceq ax-hvmulass mp3an ) AGHBGHCIHABJKCLKABCLK
      LKMDEFABCNO $.
      $( [3-Sep-1999] $)

    $( Scalar multiplication commutative law. $)
    hvmulcomi $p |- ( A .h ( B .h C ) ) = ( B .h ( A .h C ) ) $=
      ( cc wcel chil csm co wceq hvmulcom mp3an ) AGHBGHCIHABCJKJKBACJKJKLDEFAB
      CMN $.
      $( [3-Sep-1999] $)

    $( Double negative in scalar multiplication. $)
    hvmul2negi $p |- ( -u A .h ( -u B .h C ) ) = ( A .h ( B .h C ) ) $=
      ( cneg cmul co csm mul2negi oveq1i negcli hvmulassi 3eqtr3i ) AGZBGZHIZCJ
      IABHIZCJIPQCJIJIABCJIJIRSCJABDEKLPQCADMBEMFNABCDEFNO $.
      $( [3-Sep-1999] $)
  $}

  $( Scalar multiplication distributive law for subtraction. $)
  hvsubdistr1 $p |- ( ( A e. CC /\ B e. ~H /\ C e. ~H ) ->
                    ( A .h ( B -h C ) ) = ( ( A .h B ) -h ( A .h C ) ) ) $=
    ( cc wcel chil w3a c1 cneg csm co cva cmv wceq ax-1cn negcli hvmulcl oveq2d
    3adant2 hvsubval mpan ax-hvdistr1 syl3an3 wa hvmulcom eqtrd 3adant1 3adant3
    mp3an2 syl2anc 3eqtr4d ) ADEZBFEZCFEZGZABHIZCJKZLKZJKZABJKZUPACJKZJKZLKZABC
    MKZJKUTVAMKZUOUSUTAUQJKZLKZVCUNULUMUQFEZUSVGNUPDEZUNVHHOPZUPCQUAABUQUBUCULU
    NVGVCNUMULUNUDVFVBUTLULVIUNVFVBNVJAUPCUEUIRSUFUOVDURAJUMUNVDURNULBCTUGRUOUT
    FEZVAFEZVEVCNULUMVKUNABQUHULUNVLUMACQSUTVATUJUK $.
    $( [19-May-2005] $)

  $( Scalar multiplication distributive law for subtraction. $)
  hvsubdistr2 $p |- ( ( A e. CC /\ B e. CC /\ C e. ~H ) -> ( ( A - B ) .h C
      ) = ( ( A .h C ) -h ( B .h C ) ) ) $=
    ( cc wcel chil w3a cmin co csm cneg cva cmv caddc wceq negcl oveq1d 3adant1
    c1 hvmulcl ax-hvdistr2 syl3an2 negsub 3adant3 wa mulm1 adantr ax-1cn negcli
    ax-hvmulass mp3an1 eqtr3d oveq2d 3eqtr3d 3adant2 hvsubval syl2anc eqtr4d
    cmul ) ADEZBDEZCFEZGZABHIZCJIZACJIZSKZBCJIZJIZLIZVFVHMIZVCABKZNIZCJIZVFVLCJ
    IZLIZVEVJVAUTVLDEVBVNVPOBPAVLCUAUBVCVMVDCJUTVAVMVDOVBABUCUDQVCVOVIVFLVAVBVO
    VIOUTVAVBUEVGBUSIZCJIZVOVIVAVRVOOVBVAVQVLCJBUFQUGVGDEVAVBVRVIOSUHUIVGBCUJUK
    ULRUMUNVCVFFEZVHFEZVKVJOUTVBVSVAACTUOVAVBVTUTBCTRVFVHUPUQUR $.
    $( [1-Aug-2006] $) $( [19-May-2005] $)

  ${
    hvdistr1.1 $e |- A e. CC $.
    hvdistr1.2 $e |- B e. ~H $.
    hvdistr1.3 $e |- C e. ~H $.
    $( Scalar multiplication distributive law. $)
    hvdistr1i $p |- ( A .h ( B +h C ) ) = ( ( A .h B ) +h ( A .h C ) ) $=
      ( cc wcel chil cva co csm wceq ax-hvdistr1 mp3an ) AGHBIHCIHABCJKLKABLKAC
      LKJKMDEFABCNO $.
      $( [3-Sep-1999] $)

    $( Scalar multiplication distributive law. $)
    hvsubdistr1i $p |- ( A .h ( B -h C ) ) = ( ( A .h B ) -h ( A .h C ) ) $=
      ( cc wcel chil cmv co csm wceq hvsubdistr1 mp3an ) AGHBIHCIHABCJKLKABLKAC
      LKJKMDEFABCNO $.
      $( [3-Sep-1999] $)
  $}

  ${
    hvass.1 $e |- A e. ~H $.
    hvass.2 $e |- B e. ~H $.
    hvass.3 $e |- C e. ~H $.
    $( Hilbert vector space associative law. $)
    hvassi $p |- ( ( A +h B ) +h C ) = ( A +h ( B +h C ) ) $=
      ( chil wcel cva co wceq ax-hvass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABCLM
      $.
      $( [3-Sep-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvadd32i $p |- ( ( A +h B ) +h C ) = ( ( A +h C ) +h B ) $=
      ( chil wcel cva co wceq hvadd32 mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM
      $.
      $( [18-Aug-1999] $)

    $( Hilbert vector space associative law for subtraction. $)
    hvsubassi $p |- ( ( A -h B ) -h C ) = ( A -h ( B +h C ) ) $=
      ( chil wcel cmv co cva wceq hvsubass mp3an ) AGHBGHCGHABIJCIJABCKJIJLDEFA
      BCMN $.
      $( [15-May-2014] $) $( [7-Oct-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvsub32i $p |- ( ( A -h B ) -h C ) = ( ( A -h C ) -h B ) $=
      ( chil wcel cmv co wceq hvsub32 mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM
      $.
      $( [15-May-2014] $) $( [7-Oct-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvadd12i $p |- ( A +h ( B +h C ) ) = ( B +h ( A +h C ) ) $=
      ( cva co hvcomi oveq1i hvassi 3eqtr3i ) ABGHZCGHBAGHZCGHABCGHGHBACGHGHMNC
      GABDEIJABCDEFKBACEDFKL $.
      $( [11-Sep-1999] $)

    ${
      hvadd4.4 $e |- D e. ~H $.
      $( Hilbert vector space addition law. $)
      hvadd4i $p |- ( ( A +h B ) +h ( C +h D ) ) =
                   ( ( A +h C ) +h ( B +h D ) ) $=
        ( chil wcel cva co wceq hvadd4 mp4an ) AIJBIJCIJDIJABKLCDKLKLACKLBDKLKL
        MEFGHABCDNO $.
        $( [3-Sep-1999] $)

      $( Hilbert vector space addition law. $)
      hvsubsub4i $p |- ( ( A -h B ) -h ( C -h D ) ) =
                      ( ( A -h C ) -h ( B -h D ) ) $=
        ( c1 csm co cva cmv hvmulcli hvdistr1i oveq2i 3eqtr4i hvsubvali oveq12i
        hvaddcli cneg ax-1cn negcli hvadd4i ) AIUAZBJKZLKZCUEDJKZLKZMKZAUECJKZL
        KZBUHLKZMKZABMKZCDMKZMKACMKZBDMKZMKUGUEUIJKZLKZULUEUMJKZLKZUJUNUGUKUEUH
        JKZLKZLKULUFVCLKZLKUTVBAUFUKVCEUEBIUBUCZFNZUECVFGNZUEUHVFUEDVFHNZNUDUSV
        DUGLUECUHVFGVIOPVAVEULLUEBUHVFFVIOPQUGUIAUFEVGTCUHGVITRULUMAUKEVHTBUHFV
        ITRQUOUGUPUIMABEFRCDGHRSUQULURUMMACEGRBDFHRSQ $.
        $( [31-Oct-1999] $)
    $}
  $}

  $( Hilbert vector space addition/subtraction law. $)
  hvsubsub4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) )
            -> ( ( A -h B ) -h ( C -h D ) ) = ( ( A -h C ) -h ( B -h D ) ) ) $=
    ( chil wcel cmv co wceq c0v cif oveq1 oveq1d eqeq12d oveq2d ax-hv0cl elimel
    oveq2 hvsubsub4i dedth4h ) AEFZBEFZCEFZDEFZABGHZCDGHZGHZACGHZBDGHZGHZIUAAJK
    ZBGHZUFGHZUKCGHZUIGHZIUKUBBJKZGHZUFGHZUNUPDGHZGHZIUQUCCJKZDGHZGHZUKVAGHZUSG
    HZIUQVAUDDJKZGHZGHZVDUPVFGHZGHZIABCDJJJJAUKIZUGUMUJUOVKUEULUFGAUKBGLMVKUHUN
    UIGAUKCGLMNBUPIZUMURUOUTVLULUQUFGBUPUKGRMVLUIUSUNGBUPDGLONCVAIZURVCUTVEVMUF
    VBUQGCVADGLOVMUNVDUSGCVAUKGRMNDVFIZVCVHVEVJVNVBVGUQGDVFVAGROVNUSVIVDGDVFUPG
    RONUKUPVAVFAJEPQBJEPQCJEPQDJEPQST $.
    $( [2-Apr-2000] $)

  $( Two times a vector. $)
  hv2times $p |- ( A e. ~H -> ( 2 .h A ) = ( A +h A ) ) $=
    ( chil wcel c2 csm co cva caddc df-2 oveq1i wceq ax-1cn ax-hvdistr2 mp3an12
    c1 cc syl5eq ax-hvdistr1 mp3an1 anidms hvaddcl ax-hvmulid syl 3eqtr2d ) ABC
    ZDAEFZOAEFZUGGFZOAAGFZEFZUIUEUFOOHFZAEFZUHDUKAEIJOPCZUMUEULUHKLLOOAMNQUEUJU
    HKZUMUEUEUNLOAARSTUEUIBCZUJUIKUEUOAAUATUIUBUCUD $.
    $( [22-Jun-2006] $)

  ${
    hvnegdi.1 $e |- A e. ~H $.
    hvnegdi.2 $e |- B e. ~H $.
    $( Distribution of negative over subtraction. $)
    hvnegdii $p |- ( -u 1 .h ( A -h B ) ) = ( B -h A ) $=
      ( cneg cmv csm cva hvsubvali oveq2i ax-1cn negcli hvmulcli hvdistr1i cmul
      c1 co mul2negi mulid1i oveq1i hvmulassi chil wcel wceq ax-hvmulid 3eqtr3i
      eqtri ax-mp hvcomi 3eqtr4i 3eqtri ) PEZABFQZGQULAULBGQZHQZGQULAGQZULUNGQZ
      HQZBAFQZUMUOULGABCDIJULAUNPKLZCULBUTDMZNUQUPHQBUPHQURUSUQBUPHULULOQZBGQPB
      GQZUQBVBPBGVBPPOQPPPKKRPKSUGTULULBUTUTDUABUBUCVCBUDDBUEUHUFTUPUQULAUTCMUL
      UNUTVAMUIBADCIUJUK $.
      $( [31-Oct-1999] $)

    $( If the difference between two vectors is zero, they are equal. $)
    hvsubeq0i $p |- ( ( A -h B ) = 0h <-> A = B ) $=
      ( cmv co c0v wceq c1 cneg csm cva hvsubvali eqeq1i oveq1 ax-1cn chil wcel
      sylbi ax-mp negcli hvmulcli hvadd32i hvassi hvnegidi ax-hvaddid hvaddid2i
      oveq2i eqtri 3eqtri 3eqtr3g hvsubid syl6eq impbii ) ABEFZGHZABHZUPAIJZBKF
      ZLFZBLFZGBLFZABUPUTGHVAVBHUOUTGABCDMNUTGBLOSVAABLFUSLFABUSLFZLFZAAUSBCURB
      IPUADUBZDUCABUSCDVEUDVDAGLFZAVCGALBDUEUHAQRVFAHCAUFTUIUJBDUGUKUQUOBBEFZGA
      BBEOBQRVGGHDBULTUMUN $.
      $( [18-Aug-1999] $)

    $( Vector cancellation law. $)
    hvsubcan2i $p |- ( ( A +h B ) +h ( A -h B ) ) = ( 2 .h A ) $=
      ( cva co cmv cneg csm c0v hvsubvali oveq2i ax-1cn hvmulcli chil wcel wceq
      c1 c2 ax-mp negcli hvadd4i hv2times hvnegidi oveq12i eqtri 2cn ax-hvaddid
      eqcomi 3eqtri ) ABEFZABGFZEFUKARHZBIFZEFZEFZSAIFZJEFZUQULUOUKEABCDKLUPAAE
      FZBUNEFZEFURABAUNCDCUMBRMUADNUBUSUQUTJEUQUSAOPUQUSQCAUCTUIBDUDUEUFUQOPURU
      QQSAUGCNUQUHTUJ $.
      $( [3-Sep-1999] $)

    ${
      hvaddcan.3 $e |- C e. ~H $.
      $( Cancellation law for vector addition. $)
      hvaddcani $p |- ( ( A +h B ) = ( A +h C ) <-> B = C ) $=
        ( cva co wceq c1 cneg csm oveq1 ax-1cn negcli hvadd32i oveq1i hvaddid2i
        c0v 3eqtri hvmulcli hvnegidi 3eqtr3g oveq2 impbii ) ABGHZACGHZIZBCIUHUF
        JKZALHZGHZUGUJGHZBCUFUGUJGMUKAUJGHZBGHSBGHBABUJDEUIAJNODUAZPUMSBGADUBZQ
        BERTULUMCGHSCGHCACUJDFUNPUMSCGUOQCFRTUCBCAGUDUE $.
        $( [11-Sep-1999] $)

      $( Relationship between vector subtraction and addition. $)
      hvsubaddi $p |- ( ( A -h B ) = C <-> ( B +h C ) = A ) $=
        ( cmv co wceq c1 cneg csm cva hvsubvali eqeq1i ax-1cn hvmulcli hvadd12i
        c0v negcli hvnegidi oveq2i chil ax-hvaddid ax-mp 3eqtri hvaddcani eqcom
        wcel hvaddcli 3bitr3i bitri ) ABGHZCIAJKZBLHZMHZCIZBCMHZAIZUMUPCABDENOB
        UPMHZURIAURIUQUSUTAURUTABUOMHZMHASMHZABAUOEDUNBJPTEQZRVASAMBEUAUBAUCUIV
        BAIDAUDUEUFOBUPCEAUODVCUJFUGAURUHUKUL $.
        $( [11-Sep-1999] $)
    $}
  $}

  $( Distribution of negative over subtraction. $)
  hvnegdi $p |- ( ( A e. ~H /\ B e. ~H ) ->
                ( -u 1 .h ( A -h B ) ) = ( B -h A ) ) $=
    ( chil wcel c1 cneg cmv co csm wceq c0v oveq1 oveq2d oveq2 eqeq12d ax-hv0cl
    cif elimel hvnegdii dedth2h ) ACDZBCDZEFZABGHZIHZBAGHZJUCUAAKQZBGHZIHZBUGGH
    ZJUCUGUBBKQZGHZIHZUKUGGHZJABKKAUGJZUEUIUFUJUOUDUHUCIAUGBGLMAUGBGNOBUKJZUIUM
    UJUNUPUHULUCIBUKUGGNMBUKUGGLOUGUKAKCPRBKCPRST $.
    $( [2-Apr-2000] $)

  $( If the difference between two vectors is zero, they are equal. $)
  hvsubeq0 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( A -h B ) = 0h <-> A = B ) ) $=
    ( chil wcel cmv co c0v wceq wb cif oveq1 eqeq1 bibi12d oveq2 eqeq2 ax-hv0cl
    eqeq1d elimel hvsubeq0i dedth2h ) ACDZBCDZABEFZGHZABHZIUAAGJZBEFZGHZUFBHZIU
    FUBBGJZEFZGHZUFUJHZIABGGAUFHZUDUHUEUIUNUCUGGAUFBEKQAUFBLMBUJHZUHULUIUMUOUGU
    KGBUJUFENQBUJUFOMUFUJAGCPRBGCPRST $.
    $( [23-Oct-1999] $)

  $( If the sum of two vectors is zero, one is the negative of the other. $)
  hvaddeq0 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( A +h B ) = 0h <-> A = ( -u 1 .h B ) ) ) $=
    ( chil wcel wa cva co c0v wceq c1 cneg csm cmv hvaddsubval eqeq1d wb ax-1cn
    cc negcli hvmulcl mpan hvsubeq0 sylan2 bitrd ) ACDZBCDZEZABFGZHIAJKZBLGZMGZ
    HIZAUJIZUGUHUKHABNOUFUEUJCDZULUMPUIRDUFUNJQSUIBTUAAUJUBUCUD $.
    $( [10-Jun-2006] $)

  $( Cancellation law for vector addition. $)
  hvaddcan $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A +h B ) = ( A +h C ) <-> B = C ) ) $=
    ( chil wcel cva co wceq wb c0v cif oveq1 eqeq12d bibi1d oveq2 eqeq1 bibi12d
    eqeq1d ax-hv0cl elimel eqeq2d eqeq2 hvaddcani dedth3h ) ADEZBDEZCDEZABFGZAC
    FGZHZBCHZIUEAJKZBFGZULCFGZHZUKIULUFBJKZFGZUNHZUPCHZIUQULUGCJKZFGZHZUPUTHZIA
    BCJJJAULHZUJUOUKVDUHUMUIUNAULBFLAULCFLMNBUPHZUOURUKUSVEUMUQUNBUPULFORBUPCPQ
    CUTHZURVBUSVCVFUNVAUQCUTULFOUACUTUPUBQULUPUTAJDSTBJDSTCJDSTUCUD $.
    $( [18-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvaddcan2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A +h C ) = ( B +h C ) <-> A = B ) ) $=
    ( chil wcel cva co wceq wb ax-hvcom 3adant3 3adant2 eqeq12d hvaddcan bitr3d
    w3a 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIRSTPZCAFGZCBFGZHUCUDUEUFUAUGUBRSUF
    UAHTCAJKRTUGUBHSCBJLMCABNOQ $.
    $( [18-May-2005] $)

  $( Cancellation law for scalar multiplication. $)
  hvmulcan $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ B e. ~H /\ C e. ~H )
               -> ( ( A .h B ) = ( A .h C ) <-> B = C ) ) $=
    ( cc wcel cc0 wne wa chil w3a cmv co c0v wo csm wb 3adant3 hvsubeq0 hvmulcl
    wceq df-ne biorf sylbi ad2antlr 3adant1 hvsubdistr1 eqeq1d hvsubcl hvmul0or
    wn sylan2 3impb 3adant2 syl2anc 3bitr3d 3adant1r 3bitr3rd ) ADEZAFGZHZBIEZC
    IEZJBCKLZMTZAFTZVDNZBCTZABOLZACOLZTZUTVAVDVFPZVBUSVKURVAUSVEUJVKAFUAVEVDUBU
    CUDQVAVBVDVGPUTBCRUEURVAVBVFVJPUSURVAVBJZAVCOLZMTZVHVIKLZMTZVFVJVLVMVOMABCU
    FUGURVAVBVNVFPZVAVBHURVCIEVQBCUHAVCUIUKULVLVHIEZVIIEZVPVJPURVAVRVBABSQURVBV
    SVAACSUMVHVIRUNUOUPUQ $.
    $( [30-Dec-2008] $) $( [19-May-2005] $)

  $( Cancellation law for scalar multiplication. $)
  hvmulcan2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. ~H /\ C =/= 0h ) ) ->
                  ( ( A .h C ) = ( B .h C ) <-> A = B ) ) $=
    ( cc wcel chil c0v wne wa w3a csm co cmv wb hvmulcl 3adant1 3adant3r bitr3d
    wceq wo 3adant2 hvsubeq0 syl2anc cmin cc0 hvsubdistr2 eqeq1d subcl hvmul0or
    sylan 3impa df-ne biorf orcom syl6bb sylbi ad2antll subeq0 3adant3 3bitr2d
    wn ) ADEZBDEZCFEZCGHZIZJZACKLZBCKLZMLZGSZVHVISZABSZVBVCVDVKVLNZVEVBVCVDJZVH
    FEZVIFEZVNVBVDVPVCACOUAVCVDVQVBBCOPVHVIUBUCQVGVKABUDLZUESZCGSZTZVSVMVBVCVDV
    KWANVEVOVRCKLZGSZVKWAVOWBVJGABCUFUGVBVCVDWCWANZVBVCIVRDEVDWDABUHVRCUIUJUKRQ
    VCVFVSWANZVBVEWEVCVDVEVTVAZWECGULWFVSVTVSTWAVTVSUMVTVSUNUOUPUQPVBVCVSVMNVFA
    BURUSUTR $.
    $( [2-Jan-2009] $) $( [19-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvsubcan $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h B ) = ( A -h C ) <-> B = C ) ) $=
    ( chil wcel w3a cmv co wceq c1 cneg csm cva hvsubval 3adant3 3adant2 ax-1cn
    wb hvmulcl mpan eqeq12d negcli hvaddcan syl3an3 syl3an2 cc0 ax-1ne0 negne0i
    cc wne wa pm3.2i hvmulcan mp3an1 3adant1 3bitrd ) ADEZBDEZCDEZFZABGHZACGHZI
    AJKZBLHZMHZAVCCLHZMHZIZVDVFIZBCIZUTVAVEVBVGUQURVAVEIUSABNOUQUSVBVGIURACNPUA
    URUQVDDEZUSVHVIRZVCUIEZURVKJQUBZVCBSTUSUQVKVFDEZVLVMUSVOVNVCCSTAVDVFUCUDUEU
    RUSVIVJRZUQVMVCUFUJZUKURUSVPVMVQVNJQUGUHULVCBCUMUNUOUP $.
    $( [18-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvsubcan2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h C ) = ( B -h C ) <-> A = B ) ) $=
    ( chil wcel cmv co wceq wb w3a c1 csm hvsubcl 3adant3 3adant2 cc cc0 ax-1cn
    cneg hvnegdi wne wa negcli ax-1ne0 negne0i pm3.2i hvmulcan syl2anc hvsubcan
    mp3an1 eqeq12d 3bitr3d 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIUNUOUPJZKSZCAFG
    ZLGZVBCBFGZLGZHZVCVEHZUSUTVAVCDEZVEDEZVGVHIZUNUOVIUPCAMNUNUPVJUOCBMOVBPEZVB
    QUAZUBVIVJVKVLVMKRUCKRUDUEUFVBVCVEUGUJUHVAVDUQVFURUNUOVDUQHUPCATNUNUPVFURHU
    OCBTOUKCABUIULUM $.
    $( [18-May-2005] $)

  $( Subtraction of a zero vector. $)
  hvsub0 $p |- ( A e. ~H -> ( A -h 0h ) = A ) $=
    ( chil wcel c0v cmv co cva c1 cneg csm wceq ax-hv0cl hvsubval ax-1cn negcli
    mpan2 cc hvmul0 ax-mp oveq2i syl6eq ax-hvaddid eqtrd ) ABCZADEFZADGFZAUDUEA
    HIZDJFZGFZUFUDDBCUEUIKLADMPUHDAGUGQCUHDKHNOUGRSTUAAUBUC $.
    $( [2-Apr-2000] $)

  $( Relationship between vector subtraction and addition. $)
  hvsubadd $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h B ) = C <-> ( B +h C ) = A ) ) $=
    ( chil wcel cmv co cva wb c0v cif oveq1 eqeq1d eqeq2 bibi12d oveq2 ax-hv0cl
    wceq elimel hvsubaddi dedth3h ) ADEZBDEZCDEZABFGZCRZBCHGZARZIUBAJKZBFGZCRZU
    GUIRZIUIUCBJKZFGZCRZUMCHGZUIRZIUNUDCJKZRZUMURHGZUIRZIABCJJJAUIRZUFUKUHULVBU
    EUJCAUIBFLMAUIUGNOBUMRZUKUOULUQVCUJUNCBUMUIFPMVCUGUPUIBUMCHLMOCURRZUOUSUQVA
    CURUNNVDUPUTUICURUMHPMOUIUMURAJDQSBJDQSCJDQSTUA $.
    $( [30-Oct-1999] $)

  $( Hilbert vector space addition/subtraction law. $)
  hvaddsub4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
          ( ( A +h B ) = ( C +h D ) <-> ( A -h C ) = ( D -h B ) ) ) $=
    ( chil wcel wa cva co cmv wceq hvaddcl ancoms hvsub4 syldan hvsubid hvsubcl
    c0v syl 3eqtrd wb adantr adantl ad2ant2lr hvsubcan2 syl3anc anim2i ad2antlr
    simpr oveq2d ax-hvaddid adantlr adantrr simpl anim1i oveq1d adantll eqeq12d
    ad2antrr hvaddid2 bitr3d ) AEFZBEFZGZCEFZDEFZGZGZABHIZCBHIZJIZCDHIZVJJIZKZV
    IVLKZACJIZDBJIZKVHVIEFZVLEFZVJEFZVNVOUAVDVRVGABLUBVGVSVDCDLUCVCVEVTVBVFVEVC
    VTCBLMUDVIVLVJUEUFVHVKVPVMVQVDVEVKVPKVFVDVEGZVKVPBBJIZHIZVPRHIZVPVDVEVEVCGZ
    VKWCKVEVDWEVDVCVEVBVCUIUGMABCBNOWAWBRVPHVCWBRKVBVEBPUHUJVBVEWDVPKZVCVBVEGVP
    EFWFACQVPUKSULTUMVCVGVMVQKZVBVGVCWGVGVCGZVMCCJIZVQHIZRVQHIZVQVGVCWEVMWJKVGV
    EVCVEVFUNUOCDCBNOWHWIRVQHVEWIRKVFVCCPUSUPVFVCWKVQKZVEVFVCGVQEFWLDBQVQUTSUQT
    MUQURVA $.
    $( [18-May-2005] $)

