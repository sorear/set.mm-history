$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Godowski's equation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    golem1.1 $e |- A e. CH $.
    golem1.2 $e |- B e. CH $.
    golem1.3 $e |- C e. CH $.
    golem1.4 $e |- F = ( ( _|_ ` A ) vH ( A i^i B ) ) $.
    golem1.5 $e |- G = ( ( _|_ ` B ) vH ( B i^i C ) ) $.
    golem1.6 $e |- H = ( ( _|_ ` C ) vH ( C i^i A ) ) $.
    golem1.7 $e |- D = ( ( _|_ ` B ) vH ( B i^i A ) ) $.
    golem1.8 $e |- R = ( ( _|_ ` C ) vH ( C i^i B ) ) $.
    golem1.9 $e |- S = ( ( _|_ ` A ) vH ( A i^i C ) ) $.
    $( Lemma for Godowski's equation. $)
    golem1 $p |- ( f e. States -> ( ( ( f ` F ) + ( f ` G ) ) + ( f ` H ) ) =
                 ( ( ( f ` D ) + ( f ` R ) ) + ( f ` S ) ) ) $=
      ( caddc cv cst wcel cort cfv cin chj co cc wceq cch cr choccli stcl recnd
      mpi addass syl3anc addcl syl2anc addcom eqtrd oveq1d chincli add4 3eqtr4d
      syl22anc stji1i oveq12d incom fveq2i oveq2i syl6eq oveq12i 3eqtr4g ) GUAZ
      UBUCZAUDUEZABUFZUGUHZVPUEZBUDUEZBCUFZUGUHZVPUEZTUHZCUDUEZCAUFZUGUHZVPUEZT
      UHZWBBAUFZUGUHZVPUEZWGCBUFZUGUHZVPUEZTUHZVRACUFZUGUHZVPUEZTUHZHVPUEZIVPUE
      ZTUHZJVPUEZTUHDVPUEZEVPUEZTUHZFVPUEZTUHVQVRVPUEZVSVPUEZTUHZWBVPUEZWCVPUEZ
      TUHZTUHZWGVPUEZWHVPUEZTUHZTUHZXNXLTUHZXRXOTUHZTUHZXKXSTUHZTUHZWKXBVQXKXNT
      UHZXLXOTUHZTUHZXTTUHZXNXRTUHZYHTUHZYETUHZYAYFVQYGXRTUHZYHXSTUHZTUHZYKXKTU
      HZYOTUHZYJYMVQYNYQYOTVQYNXKYKTUHZYQVQXKUIUCZXNUIUCZXRUIUCZYNYSUJVQXKVQVRU
      KUCXKULUCAKUMVRVPUNUPUOZVQXNVQWBUKUCXNULUCBLUMWBVPUNUPUOZVQXRVQWGUKUCXRUL
      UCCMUMWGVPUNUPUOZXKXNXRUQURVQYTYKUIUCZYSYQUJUUCVQUUAUUBUUFUUDUUEXNXRUSUTZ
      XKYKVAUTVBVCVQYGUIUCZYHUIUCZUUBXSUIUCZYJYPUJVQYTUUAUUHUUCUUDXKXNUSUTVQXLU
      IUCZXOUIUCZUUIVQXLVQVSUKUCXLULUCABKLVDVSVPUNUPUOZVQXOVQWCUKUCXOULUCBCLMVD
      WCVPUNUPUOZXLXOUSUTZUUEVQXSVQWHUKUCXSULUCCAMKVDWHVPUNUPUOZYGYHXRXSVEVGVQU
      UFUUIYTUUJYMYRUJUUGUUOUUCUUPYKYHXKXSVEVGVFVQXQYIXTTVQYTUUKUUAUULXQYIUJUUC
      UUMUUDUUNXKXLXNXOVEVGVCVQYDYLYETVQUUAUUKUUBUULYDYLUJUUDUUMUUEUUNXNXLXRXOV
      EVGVCVFVQWFXQWJXTTVQWAXMWEXPTABVPKLVHBCVPLMVHVICAVPMKVHVIVQWRYDXAYETVQWNY
      BWQYCTVQWNXNWLVPUEZTUHYBBAVPLKVHUUQXLXNTWLVSVPBAVJVKVLVMVQWQXRWOVPUEZTUHY
      CCBVPMLVHUURXOXRTWOWCVPCBVJVKVLVMVIVQXAXKWSVPUEZTUHYEACVPKMVHUUSXSXKTWSWH
      VPACVJVKVLVMVIVFXEWFXFWJTXCWAXDWETHVTVPNVKIWDVPOVKVNJWIVPPVKVNXIWRXJXATXG
      WNXHWQTDWMVPQVKEWPVPRVKVNFWTVPSVKVNVO $.
      $( [10-Nov-2002] $)

    $( Lemma for Godowski's equation. $)
    golem2 $p |- ( f e. States ->
              ( ( f ` ( ( F i^i G ) i^i H ) ) = 1 -> ( f ` D ) = 1 ) ) $=
      ( cfv cv cst wcel cin c1 wceq caddc co c3 cort chj choccli chincli chjcli
      cch eqeltri stm1add3i golem1 eqeq1d sylibd stadd3i syld ) GUAZUBUCZHIUDJU
      DVCTUEUFZDVCTZEVCTUGUHFVCTUGUHZUIUFZVFUEUFVDVEHVCTIVCTUGUHJVCTUGUHZUIUFVH
      HIJVCHAUJTZABUDZUKUHUONVJVKAKULZABKLUMUNUPIBUJTZBCUDZUKUHUOOVMVNBLULZBCLM
      UMUNUPJCUJTZCAUDZUKUHUOPVPVQCMULZCAMKUMUNUPUQVDVIVGUIABCDEFGHIJKLMNOPQRSU
      RUSUTDEFVCDVMBAUDZUKUHUOQVMVSVOBALKUMUNUPEVPCBUDZUKUHUORVPVTVRCBMLUMUNUPF
      VJACUDZUKUHUOSVJWAVLACKMUMUNUPVAVB $.
      $( [13-Nov-1999] $)
  $}

  ${
    $d f F $.  $d f G $.  $d f H $.  $d f D $.
    goeq.1 $e |- A e. CH $.
    goeq.2 $e |- B e. CH $.
    goeq.3 $e |- C e. CH $.
    goeq.4 $e |- F = ( ( _|_ ` A ) vH ( A i^i B ) ) $.
    goeq.5 $e |- G = ( ( _|_ ` B ) vH ( B i^i C ) ) $.
    goeq.6 $e |- H = ( ( _|_ ` C ) vH ( C i^i A ) ) $.
    goeq.7 $e |- D = ( ( _|_ ` B ) vH ( B i^i A ) ) $.
    $( Godowski's equation, shown here as a variant equivalent to Equation SF
       of [Godowski] p. 730. $)
    goeqi $p |- ( ( F i^i G ) i^i H ) C_ D $=
      ( vf cin cfv chj co chincli cv c1 wceq wi wss cst cort cch choccli chjcli
      eqeltri stri eqid golem2 mprg ) EFPZGPZOUAZQUBUCDURQUBUCUDUQDUEOUFUQDOUPG
      EFEAUGQZABPZRSUHKUSUTAHUIABHITUJUKFBUGQZBCPZRSUHLVAVBBIUIZBCIJTUJUKTGCUGQ
      ZCAPZRSUHMVDVECJUICAJHTUJUKTDVABAPZRSUHNVAVFVCBAIHTUJUKULABCDVDCBPRSZUSAC
      PRSZOEFGHIJKLMNVGUMVHUMUNUO $.
      $( [10-Nov-2002] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y S $.
    stcltr1.1 $e |- ( ph <-> ( S e. States /\ A. x e. CH A. y e. CH
                ( ( ( S ` x ) = 1 -> ( S ` y ) = 1 ) -> x C_ y ) ) ) $.
    stcltr1.2 $e |- A e. CH $.
    ${
      stcltr1.3 $e |- B e. CH $.
      $( Property of a strong classical state. $)
      stcltr1i $p |- ( ph ->
               ( ( ( S ` A ) = 1 -> ( S ` B ) = 1 ) -> A C_ B ) ) $=
        ( wcel cv cfv c1 wceq wi wss cch wral fveq2 eqeq1d cst wa sseq1 imbi12d
        imbi1d imbi2d sseq2 rcla42v mp2an adantl sylbi ) AFUAJZBKZFLZMNZCKZFLZM
        NZOZUMUPPZOZCQRBQRZUBDFLZMNZEFLZMNZOZDEPZOZGVBVIULDQJEQJVBVIOHIVAVIVDUR
        OZDUPPZOBCDEQQUMDNZUSVJUTVKVLUOVDURVLUNVCMUMDFSTUEUMDUPUCUDUPENZVJVGVKV
        HVMURVFVDVMUQVEMUPEFSTUFUPEDUGUDUHUIUJUK $.
        $( [24-Oct-1999] $)
    $}

    $( Property of a strong classical state. $)
    stcltr2i $p |- ( ph -> ( ( S ` A ) = 1 -> A = ~H ) ) $=
      ( cfv c1 wceq chil wss wi ax-1 helch stcltr1i syl5 chssii eqss mpbiran
      syl6ibr ) ADEHIJZKDLZDKJZUBKEHIJZUBMAUCUBUENABCKDEFOGPQUDDKLUCDGRDKSTUA
      $.
      $( [24-Oct-1999] $)

    ${
      stcltrlem1.3 $e |- B e. CH $.
      $( Lemma for strong classical state theorem. $)
      stcltrlem1 $p |- ( ph -> ( ( S ` B ) = 1 ->
                    ( S ` ( ( _|_ ` A ) vH ( A i^i B ) ) ) = 1 ) ) $=
        ( cfv c1 wceq cin co caddc wcel cv cch syl chil cort chj wa cst wi wral
        wss simplbi stji1i adantr stcltr2i imp ineq2 chm1i syl6eq fveq2d oveq2d
        cc cr choccli stcl mpi recnd addcom syl2anc sto1i eqtrd 3eqtrd ex ) AEF
        JKLZDUAJZDEMZUBNFJZKLAVJUCZVMVKFJZVLFJZONZVODFJZONZKAVMVQLZVJAFUDPZVTAW
        ABQZFJKLCQZFJKLUEWBWCUGUECRUFBRUFGUHZDEFHIUISUJVNVPVRVOOVNVLDFVNETLZVLD
        LAVJWEABCEFGIUKULWEVLDTMDETDUMDHUNUOSUPUQVNWAVSKLAWAVJWDUJWAVSVRVOONZKW
        AVOURPVRURPVSWFLWAVOWAVKRPVOUSPDHUTVKFVAVBVCWAVRWADRPVRUSPHDFVAVBVCVOVR
        VDVEDFHVFVGSVHVI $.
        $( [24-Oct-1999] $)

      $( Lemma for strong classical state theorem. $)
      stcltrlem2 $p |- ( ph -> B C_ ( ( _|_ ` A ) vH ( A i^i B ) ) ) $=
        ( cfv c1 wceq cort cin chj co wi wss stcltrlem1 choccli chjcli stcltr1i
        chincli mpd ) AEFJKLDMJZDENZOPZFJKLQEUGRABCDEFGHISABCEUGFGIUEUFDHTDEHIU
        CUAUBUD $.
        $( [24-Oct-1999] $)
    $}
  $}

  ${
    $d x y s A $.  $d x y s B $.
    stcltrth.1 $e |- A e. CH $.
    stcltrth.2 $e |- B e. CH $.
    stcltrth.3 $e |- E. s e. States A. x e. CH A. y e. CH
                ( ( ( s ` x ) = 1 -> ( s ` y ) = 1 ) -> x C_ y ) $.
    $( Theorem for classically strong set of states.  If there exists a
       "classically strong set of states" on lattice ` CH ` (or actually any
       ortholattice, which would have an identical proof), then any two
       elements of the lattice commute, i.e., the lattice is distributive.
       (Proof due to Mladen Pavicic.)  Theorem 3.3 of [MegPav2000] p. 2344. $)
    stcltrthi $p |- B C_ ( ( _|_ ` A ) vH ( A i^i B ) ) $=
      ( cv cfv c1 wceq wi wss cch wral cst wrex cort cin chj co wcel stcltrlem2
      wa biid rexlimiva ax-mp ) AIZEIZJKLBIZUJJKLMUIUKNMBOPAOPZEQRDCSJCDTUAUBNZ
      HULUMEQUJQUCULUEZABCDUJUNUFFGUDUGUH $.
      $( [24-Oct-1999] $)
  $}

