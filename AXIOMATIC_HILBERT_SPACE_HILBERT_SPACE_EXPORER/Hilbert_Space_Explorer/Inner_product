$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Inner product
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Associative law for inner product.  Lemma 3.1(S5) of [Beran] p. 95. $)
  his5 $p |- ( ( A e. CC /\ B e. ~H /\ C e. ~H ) ->
                ( B .ih ( A .h C ) ) = ( ( * ` A ) x. ( B .ih C ) ) ) $=
    ( cc wcel chil w3a csm co csp ccj cmul wceq wa hvmulcl ax-his1 sylan2 3impb
    cfv 3com23 3com12 ax-his3 fveq2d hicl cjmul 3adant1 oveq2d eqtr4d 3eqtrd )
    ADEZBFEZCFEZGZBACHIZJIZUNBJIZKSZACBJIZLIZKSZAKSZBCJIZLIZUKUJULUOUQMZUKUJULV
    DUJULNUKUNFEVDACOBUNPQRUAUMUPUSKUJULUKUPUSMACBUBTUCUMUTVAURKSZLIZVCUJULUKUT
    VFMZUJULUKVGULUKNUJURDEVGCBUDAURUEQRTUMVBVEVALUKULVBVEMUJBCPUFUGUHUI $.
    $( [29-Jul-1999] $)

  $( Associative law for inner product. $)
  his52 $p |- ( ( A e. CC /\ B e. ~H /\ C e. ~H ) ->
                ( B .ih ( ( * ` A ) .h C ) ) = ( A x. ( B .ih C ) ) ) $=
    ( cc wcel chil w3a ccj cfv csm co cmul wceq cjcl his5 syl3an1 cjcj 3ad2ant1
    csp oveq1d eqtrd ) ADEZBFEZCFEZGBAHIZCJKSKZUEHIZBCSKZLKZAUHLKZUBUEDEUCUDUFU
    IMANUEBCOPUBUCUIUJMUDUBUGAUHLAQTRUA $.
    $( [13-Feb-2006] $)
  $( Move scalar multiplication to outside of inner product. $)
  his35 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. ~H /\ D e. ~H ) ) ->
                ( ( A .h C ) .ih ( B .h D ) ) =
                  ( ( A x. ( * ` B ) ) x. ( C .ih D ) ) ) $=
    ( cc wcel wa chil csm co csp cmul ccj cfv wceq 3expb adantll oveq2d syl3anc
    his5 simpll simprl hvmulcl ad2ant2l ax-his3 cjcl hicl adantl mulass 3eqtr4d
    ad2antlr ) AEFZBEFZGZCHFZDHFZGZGZACBDIJZKJZLJZABMNZCDKJZLJZLJZACIJUSKJZAVBL
    JVCLJZURUTVDALUMUQUTVDOZULUMUOUPVHBCDTPQRURULUOUSHFZVFVAOULUMUQUAZUNUOUPUBU
    MUPVIULUOBDUCUDACUSUESURULVBEFZVCEFZVGVEOVJUMVKULUQBUFUKUQVLUNCDUGUHAVBVCUI
    SUJ $.
    $( [15-May-2014] $)

  ${
    his35.1 $e |- A e. CC $.
    his35.2 $e |- B e. CC $.
    his35.3 $e |- C e. ~H $.
    his35.4 $e |- D e. ~H $.
    $( Move scalar multiplication to outside of inner product. $)
    his35i $p |- ( ( A .h C ) .ih ( B .h D ) ) =
              ( ( A x. ( * ` B ) ) x. ( C .ih D ) ) $=
      ( cc wcel chil csm co csp ccj cfv cmul wceq his35 mp4an ) AIJBIJCKJDKJACL
      MBDLMNMABOPQMCDNMQMREFGHABCDST $.
      $( [15-May-2014] $) $( [1-Jul-2005] $)
  $}

  $( Distributive law for inner product.  Lemma 3.1(S7) of [Beran] p. 95. $)
  his7 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                ( A .ih ( B +h C ) ) = ( ( A .ih B ) + ( A .ih C ) ) ) $=
    ( chil wcel w3a cva co csp ccj caddc wceq ax-his2 fveq2d wa cc hicl ax-his1
    cfv cjadd syl2an 3impdir eqtrd 3comr hvaddcl sylan2 3adant3 3adant2 oveq12d
    3impb 3eqtr4d ) ADEZBDEZCDEZFZBCGHZAIHZJSZBAIHZJSZCAIHZJSZKHZAUPIHZABIHZACI
    HZKHUMUNULURVCLUMUNULFZURUSVAKHZJSZVCVGUQVHJBCAMNUMULUNVIVCLZUMULOUSPEVAPEV
    JUNULOBAQCAQUSVATUAUBUCUDULUMUNVDURLZUMUNOULUPDEVKBCUEAUPRUFUJUOVEUTVFVBKUL
    UMVEUTLUNABRUGULUNVFVBLUMACRUHUIUK $.
    $( [31-Jul-1999] $)

  $( Distributive/associative law for inner product, useful for linearity
     proofs. $)
  hiassdi $p |- ( ( ( A e. CC /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
    ( ( ( A .h B ) +h C ) .ih D ) = ( ( A x. ( B .ih D ) ) + ( C .ih D ) ) ) $=
    ( cc wcel chil wa csm cva csp caddc cmul wceq hvmulcl ax-his2 3expb ax-his3
    co sylan 3expa adantrl oveq1d eqtrd ) AEFZBGFZHZCGFZDGFZHZHZABISZCJSDKSZULD
    KSZCDKSZLSZABDKSMSZUOLSUGULGFZUJUMUPNZABOURUHUIUSULCDPQTUKUNUQUOLUGUIUNUQNZ
    UHUEUFUIUTABDRUAUBUCUD $.
    $( [10-May-2005] $)

  $( Distributive law for inner product of vector subtraction. $)
  his2sub $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                ( ( A -h B ) .ih C ) = ( ( A .ih C ) - ( B .ih C ) ) ) $=
    ( chil wcel w3a cmv co csp c1 cneg csm cva caddc wceq wa hicl eqtrd 3adant1
    cc cmin hvsubval oveq1d 3adant3 ax-1cn hvmulcl mpan ax-his2 syl3an2 ax-his3
    negcli cmul mp3an1 mulm1 syl oveq2d 3adant2 negsub syl2anc 3eqtrd ) ADEZBDE
    ZCDEZFZABGHZCIHZAJKZBLHZMHZCIHZACIHZBCIHZKZNHZVKVLUAHZVAVBVFVJOVCVAVBPVEVIC
    IABUBUCUDVDVJVKVHCIHZNHZVNVBVAVHDEZVCVJVQOVGTEZVBVRJUEUKZVGBUFUGAVHCUHUIVBV
    CVQVNOVAVBVCPZVPVMVKNWAVPVGVLULHZVMVSVBVCVPWBOVTVGBCUJUMWAVLTEZWBVMOBCQZVLU
    NUORUPSRVDVKTEZWCVNVOOVAVCWEVBACQUQVBVCWCVAWDSVKVLURUSUT $.
    $( [1-Aug-2006] $) $( [16-Nov-1999] $)

  $( Distributive law for inner product of vector subtraction. $)
  his2sub2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                ( A .ih ( B -h C ) ) = ( ( A .ih B ) - ( A .ih C ) ) ) $=
    ( chil wcel w3a cmv co csp ccj cfv cmin wceq his2sub fveq2d wa hicl ax-his1
    cc cjsub syl2an 3impdir eqtrd 3comr hvsubcl 3adant3 3adant2 oveq12d 3eqtr4d
    sylan2 3impb ) ADEZBDEZCDEZFZBCGHZAIHZJKZBAIHZJKZCAIHZJKZLHZAUPIHZABIHZACIH
    ZLHUMUNULURVCMUMUNULFZURUSVALHZJKZVCVGUQVHJBCANOUMULUNVIVCMZUMULPUSSEVASEVJ
    UNULPBAQCAQUSVATUAUBUCUDULUMUNVDURMZUMUNPULUPDEVKBCUEAUPRUJUKUOVEUTVFVBLULU
    MVEUTMUNABRUFULUNVFVBMUMACRUGUHUI $.
    $( [13-Feb-2006] $)

  $( A necessary and sufficient condition for an inner product to be real. $)
  hire $p |- ( ( A e. ~H /\ B e. ~H ) ->
              ( ( A .ih B ) e. RR <-> ( A .ih B ) = ( B .ih A ) ) ) $=
    ( chil wcel wa csp co cr ccj cfv wceq cc wb hicl cjreb eqcom syl6bb ax-his1
    syl ancoms eqeq2d bitr4d ) ACDZBCDZEZABFGZHDZUFUFIJZKZUFBAFGZKUEUGUHUFKZUIU
    EUFLDUGUKMABNUFOSUHUFPQUEUJUHUFUDUCUJUHKBARTUAUB $.
    $( [2-Jul-2005] $)

  $( Real closure of inner product with self. $)
  hiidrcl $p |- ( A e. ~H -> ( A .ih A ) e. RR ) $=
    ( chil wcel csp co cr wa wceq eqid hire mpbiri anidms ) ABCZAADEZFCZMMGONNH
    NIAAJKL $.
    $( [29-May-1999] $)

  $( Inner product with the 0 vector. $)
  hi01 $p |- ( A e. ~H -> ( 0h .ih A ) = 0 ) $=
    ( chil wcel c0v csp co cc0 cmul csm wceq ax-hv0cl ax-hvmul0 ax-mp oveq1i cc
    0cn ax-his3 mp3an12 syl5eqr hicl mpan mul02 syl eqtrd ) ABCZDAEFZGUFHFZGUEU
    FGDIFZAEFZUGUHDAEDBCZUHDJKDLMNGOCUJUEUIUGJPKGDAQRSUEUFOCZUGGJUJUEUKKDATUAUF
    UBUCUD $.
    $( [29-May-1999] $)

  $( Inner product with the 0 vector. $)
  hi02 $p |- ( A e. ~H -> ( A .ih 0h ) = 0 ) $=
    ( chil wcel c0v csp ccj cfv cc0 wceq ax-hv0cl ax-his1 mpan2 hi01 fveq2d cj0
    co syl6eq eqtrd ) ABCZADEPZDAEPZFGZHSDBCTUBIJADKLSUBHFGHSUAHFAMNOQR $.
    $( [13-Oct-1999] $)

  $( Inner product with self is not negative. $)
  hiidge0 $p |- ( A e. ~H -> 0 <_ ( A .ih A ) ) $=
    ( chil wcel cc0 csp co cle wbr clt wceq wo c0v pm2.1 df-ne ax-his4 sylan2br
    wn wne ex cr wa oveq1 hi01 sylan9eqr eqcomd orim12d mpi 0re hiidrcl sylancr
    wb leloe mpbird ) ABCZDAAEFZGHZDUOIHZDUOJZKZUNALJZQZUTKUSUTMUNVAUQUTURUNVAU
    QVAUNALRUQALNAOPSUNUTURUNUTUAUODUTUNUOLAEFDALAEUBAUCUDUESUFUGUNDTCUOTCUPUSU
    KUHAUIDUOULUJUM $.
    $( [29-May-1999] $)

  $( Zero inner product with self means vector is zero.  Lemma 3.1(S6) of
     [Beran] p. 95. $)
  his6 $p |- ( A e. ~H -> ( ( A .ih A ) = 0 <-> A = 0h ) ) $=
    ( chil wcel csp co cc0 c0v wne wa clt wbr ax-his4 wb cr cle hiidrcl hiidge0
    wceq 0re leltne mp3an1 syl2anc adantr mpbid necon4d oveq1 eqeq1d syl5ibrcom
    ex hi01 impbid ) ABCZAADEZFRZAGRZULAGUMFULAGHZUMFHZULUPIFUMJKZUQALULURUQMZU
    PULUMNCZFUMOKZUSAPAQFNCUTVAUSSFUMTUAUBUCUDUIUEULUNUOGADEZFRAUJUOUMVBFAGADUF
    UGUHUK $.
    $( [27-Jul-1999] $)

  ${
    his1.1 $e |- A e. ~H $.
    his1.2 $e |- B e. ~H $.
    $( Conjugate law for inner product.  Postulate (S1) of [Beran] p. 95. $)
    his1i $p |- ( A .ih B ) = ( * ` ( B .ih A ) ) $=
      ( chil wcel csp co ccj cfv wceq ax-his1 mp2an ) AEFBEFABGHBAGHIJKCDABLM
      $.
      $( [15-May-2005] $)
  $}

  $( Commuted inner products have the same absolute values. $)
  abshicom $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( abs ` ( A .ih B ) ) = ( abs ` ( B .ih A ) ) ) $=
    ( chil wcel wa csp co cabs cfv ccj ax-his1 fveq2d cc wceq hicl ancoms abscj
    syl eqtrd ) ACDZBCDZEZABFGZHIBAFGZJIZHIZUDHIZUBUCUEHABKLUBUDMDZUFUGNUATUHBA
    OPUDQRS $.
    $( [26-May-2006] $)

  ${
    $d x A $.
    $( A vector whose inner product is always zero is zero. $)
    hial0 $p |- ( A e. ~H -> ( A. x e. ~H ( A .ih x ) = 0 <-> A = 0h ) ) $=
      ( chil wcel cv csp co cc0 wceq wral oveq2 eqeq1d rcla4v his6 sylibd oveq1
      c0v wi hi01 sylan9eq ex a1i ralrimdv impbid ) BCDZBAEZFGZHIZACJZBQIZUEUIB
      BFGZHIZUJUHULABCUFBIUGUKHUFBBFKLMBNOUEUJUHACUJUFCDZUHRRUEUJUMUHUJUMUGQUFF
      GHBQUFFPUFSTUAUBUCUD $.
      $( [24-Oct-1999] $)

    $( A vector whose inner product is always zero is zero. $)
    hial02 $p |- ( A e. ~H -> ( A. x e. ~H ( x .ih A ) = 0 <-> A = 0h ) ) $=
      ( chil wcel cv csp co cc0 wceq wral oveq1 eqeq1d rcla4v his6 sylibd oveq2
      c0v wi hi02 sylan9eq ex a1i ralrimdv impbid ) BCDZAEZBFGZHIZACJZBQIZUEUIB
      BFGZHIZUJUHULABCUFBIUGUKHUFBBFKLMBNOUEUJUHACUJUFCDZUHRRUEUJUMUHUJUMUGUFQF
      GHBQUFFPUFSTUAUBUCUD $.
      $( [28-Jan-2006] $)
  $}

  ${
    hisubcom.1 $e |- A e. ~H $.
    hisubcom.2 $e |- B e. ~H $.
    hisubcom.3 $e |- C e. ~H $.
    hisubcom.4 $e |- D e. ~H $.
    $( Two vector subtractions simultaneously commute in an inner product. $)
    hisubcomi $p |- ( ( A -h B ) .ih ( C -h D ) ) =
                   ( ( B -h A ) .ih ( D -h C ) ) $=
      ( c1 cneg cmv co csm csp hvnegdii oveq12i cmul ax-1cn hvsubcli 3eqtri ccj
      cfv negcli his35i cr wcel wceq 1re renegcli ax-mp oveq2i mul2negi mulid1i
      cjre oveq1i hicli mulid2i eqtr3i ) IJZBAKLZMLZUSDCKLZMLZNLZABKLZCDKLZNLUT
      VBNLZVAVEVCVFNBAFEODCHGOPVDUSUSUAUBZQLZVGQLIVGQLVGUSUSUTVBIRUCZVJBAFESZDC
      HGSZUDVIIVGQVIUSUSQLIIQLIVHUSUSQUSUEUFVHUSUGIUHUIUSUNUJUKIIRRULIRUMTUOVGU
      TVBVKVLUPUQTUR $.
      $( [1-Jul-2005] $)
  $}

  $( Lemma used to prove equality of vectors. $)
  hi2eq $p |- ( ( A e. ~H /\ B e. ~H ) ->
               ( ( A .ih ( A -h B ) ) = ( B .ih ( A -h B ) ) <-> A = B ) ) $=
    ( chil wcel wa cmv co csp cmin cc0 c0v hvsubcl his2sub mpd3an3 eqeq1d wb cc
    wceq hicl syl2anc his6 syl bitr3d syldan simpr subeq0 hvsubeq0 3bitr3d ) AC
    DZBCDZEZAABFGZHGZBULHGZIGZJRZULKRZUMUNRZABRUKULULHGZJRZUPUQUKUSUOJUIUJULCDZ
    USUORABLZABULMNOUKVAUTUQPVBULUAUBUCUKUMQDZUNQDZUPURPUIUJVAVCVBAULSUDUKUJVAV
    DUIUJUEVBBULSTUMUNUFTABUGUH $.
    $( [16-Nov-1999] $)

  ${
    $d x A $.  $d x B $.
    $( Two vectors whose inner product is always equal are equal. $)
    hial2eq $p |- ( ( A e. ~H /\ B e. ~H ) ->
                   ( A. x e. ~H ( A .ih x ) = ( B .ih x ) <-> A = B ) ) $=
      ( chil wcel wa cv csp co wceq wral cmv hvsubcl oveq2 eqeq12d rcla4v hi2eq
      wi syl sylibd oveq1 ralrimivw impbid1 ) BDECDEFZBAGZHIZCUEHIZJZADKZBCJZUD
      UIBBCLIZHIZCUKHIZJZUJUDUKDEUIUNRBCMUHUNAUKDUEUKJUFULUGUMUEUKBHNUEUKCHNOPS
      BCQTUJUHADBCUEHUAUBUC $.
      $( [16-Nov-1999] $)

    $( Two vectors whose inner product is always equal are equal. $)
    hial2eq2 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                   ( A. x e. ~H ( x .ih A ) = ( x .ih B ) <-> A = B ) ) $=
      ( chil wcel wa cv csp co wceq wral wb ccj cfv ax-his1 eqeqan12d cc ancoms
      hicl cj11 syl2an bitr2d anandirs ralbidva hial2eq bitrd ) BDEZCDEZFZAGZBH
      IZUJCHIZJZADKBUJHIZCUJHIZJZADKBCJUIUMUPADUGUHUJDEZUMUPLUGUQFZUHUQFZFUPUKM
      NZULMNZJZUMURUSUNUTUOVABUJOCUJOPURUKQEZULQEZVBUMLUSUQUGVCUJBSRUQUHVDUJCSR
      UKULTUAUBUCUDABCUEUF $.
      $( [28-Jan-2006] $)
  $}

  $( Orthogonality commutes. $)
  orthcom $p |- ( ( A e. ~H /\ B e. ~H ) ->
                ( ( A .ih B ) = 0 <-> ( B .ih A ) = 0 ) ) $=
    ( chil wcel wa csp cc0 wceq ccj cfv fveq2 cj0 syl6eq ax-his1 ancoms syl5ibr
    co eqeq1d impbid ) ACDZBCDZEZABFQZGHZBAFQZGHZUDUFUBUCIJZGHUDUGGIJZGUCGIKLMU
    BUEUGGUATUEUGHBANORPUFUDUBUEIJZGHUFUIUHGUEGIKLMUBUCUIGABNRPS $.
    $( [10-Oct-1999] $)

  ${
    normlem1.1 $e |- S e. CC $.
    normlem1.2 $e |- F e. ~H $.
    normlem1.3 $e |- G e. ~H $.
    $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
       [Beran] p. 97. $)
    normlem0 $p |- ( ( F -h ( S .h G ) ) .ih
                         ( F -h ( S .h G ) ) ) =
                ( ( ( F .ih F ) + ( -u ( * ` S ) x. ( F .ih G ) ) ) +
  ( ( -u S x. ( G .ih F ) ) + ( ( S x. ( * ` S ) ) x. ( G .ih G ) ) ) ) $=
      ( csm co csp cneg cva caddc cmul oveq1i oveq2i chil wcel wceq mp3an eqtri
      cmv ccj cfv hvmulcli hvsubvali mulm1i ax-1cn negcli eqtr3i eqtr4i oveq12i
      c1 hvmulassi hvaddcli ax-his2 his7 his5 cjnegi ax-his3 hicli cjcli mulcli
      cc adddii mulassi mul2negi 3eqtri ) BACGHZUAHZVIIHBAJZCGHZKHZVLIHZBVLIHZV
      KVLIHZLHZBBIHZAUBUCZJZBCIHZMHZLHZVJCBIHZMHZAVRMHZCCIHZMHZLHZLHVIVLVIVLIVI
      BULJZVHGHZKHVLBVHEACDFUDUEVKWJBKWIAMHZCGHVKWJWKVJCGADUFNWIACULUGUHDFUMUIO
      UJZWLUKBPQZVKPQZVLPQZVMVPREVJCADUHZFUDZBVKEWQUNZBVKVLUOSVNWBVOWHLVNVQBVKI
      HZLHZWBWMWMWNVNWTREEWQBBVKUPSWSWAVQLWSVJUBUCZVTMHZWAVJVCQZWMCPQZWSXBRWPEF
      VJBCUQSXAVSVTMADURZNTOTVOVJCVLIHZMHZVJWCXAWFMHZLHZMHZWHXCXDWOVOXGRWPFWRVJ
      CVLUSSXFXIVJMXFWCCVKIHZLHZXIXDWMWNXFXLRFEWQCBVKUPSXKXHWCLXCXDXDXKXHRWPFFV
      JCCUQSOTOXJWDVJXHMHZLHWHVJWCXHWPCBFEUTXAWFVJWPVAZCCFFUTZVBVDXMWGWDLVJXAMH
      ZWFMHXMWGVJXAWFWPXNXOVEXPWEWFMXPVJVSMHWEXAVSVJMXEOAVRDADVAVFTNUIOTVGUKVG
      $.
      $( [7-Oct-1999] $)

    ${
      normlem1.4 $e |- R e. RR $.
      normlem1.5 $e |- ( abs ` S ) = 1 $.
      $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
         [Beran] p. 97. $)
      normlem1 $p |- ( ( F -h ( ( S x. R ) .h G ) ) .ih
                         ( F -h ( ( S x. R ) .h G ) ) ) =
                ( ( ( F .ih F ) + ( ( ( * ` S ) x. -u R ) x. ( F .ih G ) ) ) +
  ( ( ( S x. -u R ) x. ( G .ih F ) ) + ( ( R ^ 2 ) x. ( G .ih G ) ) ) ) $=
        ( cmul co csp cfv cneg caddc c2 cexp oveq2i oveq1i c1 csm cmv ccj recni
        mulcli normlem0 cjmuli cr wcel wceq cjrebi eqtri negeqi mulneg2i eqtr4i
        mpbi cjcli eqcomi mul4i absvalsqi 3eqtr3i oveq12i mulid2i 3eqtri sqvali
        cabs sq1 ) CBAJKZDUAKUBKZVILKCCLKZVHUCMZNZCDLKZJKZOKZVHNZDCLKZJKZVHVKJK
        ZDDLKZJKZOKZOKVJBUCMZANZJKZVMJKZOKZBWDJKZVQJKZAPQKZVTJKZOKZOKVHCDBAEAHU
        DZUEFGUFVOWGWBWLOVNWFVJOVLWEVMJVLWCAJKZNWEVKWNVKWCAUCMZJKZWNBAEWMUGZWOA
        WCJAUHUIWOAUJHAWMUKUPZRULUMWCABEUQZWMUNUOSRVRWIWAWKOVPWHVQJWHVPBAEWMUNU
        RSVSWJVTJVSAAJKZWJVSVHWPJKBWCJKZAWOJKZJKZWTVKWPVHJWQRBAWCWOEWMWSAWMUQUS
        XCTWTJKWTXATXBWTJBVFMZPQKTPQKXATXDTPQISBEUTVGVAWOAAJWRRVBWTAAWMWMUEVCUL
        VDAWMVEUOSVBVBUL $.
        $( [22-Aug-1999] $)
    $}

    ${
      normlem2.4 $e |- B = -u ( ( ( * ` S ) x. ( F .ih G ) ) +
                    ( S x. ( G .ih F ) ) ) $.
      $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
         [Beran] p. 97. $)
      normlem2 $p |- B e. RR $=
        ( ccj cfv csp co cmul caddc cr his1i oveq12i hicli cjmuli eqtr4i eqcomi
        cneg wcel wceq cjcji cjcli oveq2i mulcli addcomi cjaddi 3eqtr4ri addcli
        cjrebi mpbir renegcli eqeltri ) ABIJZCDKLZMLZBDCKLZMLZNLZUBOHVBVBOUCVBI
        JZVBUDVAUSNLUSIJZVAIJZNLVBVCVAVDUSVENVAUQIJZURIJZMLVDBVFUTVGMVFBBEUEUAD
        CGFPQUQURBEUFZCDFGRZSTUSUQUTIJZMLVEURVJUQMCDFGPUGBUTEDCGFRZSTQUSVAUQURV
        HVIUHZBUTEVKUHZUIUSVAVLVMUJUKVBUSVAVLVMULUMUNUOUP $.
        $( [27-Jul-1999] $)

      normlem3.5 $e |- A = ( G .ih G ) $.
      normlem3.6 $e |- C = ( F .ih F ) $.
      ${
        normlem3.7 $e |- R e. RR $.
        $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
           [Beran] p. 97. $)
        normlem3 $p |- ( ( ( A x. ( R ^ 2 ) ) + ( B x. R ) ) + C ) =
                ( ( ( F .ih F ) + ( ( ( * ` S ) x. -u R ) x. ( F .ih G ) ) ) +
  ( ( ( S x. -u R ) x. ( G .ih F ) ) + ( ( R ^ 2 ) x. ( G .ih G ) ) ) ) $=
          ( co cmul caddc csp hicli mulcli c2 cexp ccj cneg eqeltri recni sqcli
          cfv cc normlem2 addcomi cjcli addcli mulneg1i oveq1i mulneg2i 3eqtr4i
          negcli adddiri mul32i oveq12i 3eqtri mulcomi oveq2i eqtr3i addassi )
          CADUAUBOZPOZBDPOZQOZQOFFROZEUCUHZDUDZPOZFGROZPOZEVMPOZGFROZPOZVGGGROZ
          POZQOZQOZQOVJCQOVKVPQOWBQOCVKVJWCQMVJVIVHQOVPVSQOZWAQOWCVHVIAVGAVTUIL
          GGJJSZUEZDDNUFZUGZTZBDBBEFGHIJKUJUFWGTZUKVIWDVHWAQVIVLVOPOZEVRPOZQOZV
          MPOZWKVMPOZWLVMPOZQOWDWMUDZDPOWMDPOUDVIWNWMDWKWLVLVOEHULZFGIJSZTZEVRH
          GFJISZTZUMZWGUNBWQDPKUOWMDXCWGUPUQWKWLVMWTXBDWGURZUSWOVPWPVSQVLVOVMWR
          WSXDUTEVRVMHXAXDUTVAVBVGAPOVHWAVGAWHWFVCAVTVGPLVDVEVAVPVSWAVNVOVLVMWR
          XDTWSTZVQVREVMHXDTXATZVGVTWHWETZVFVBVAVJCVHVIWIWJUMCVKUIMFFIISZUEUKVK
          VPWBXHXEVSWAXFXGUMVFUQ $.
          $( [21-Aug-1999] $)
      $}

      ${
        normlem4.7 $e |- R e. RR $.
        normlem4.8 $e |- ( abs ` S ) = 1 $.
        $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
           [Beran] p. 97. $)
        normlem4 $p |- ( ( F -h ( ( S x. R ) .h G ) ) .ih
                       ( F -h ( ( S x. R ) .h G ) ) ) =
                       ( ( ( A x. ( R ^ 2 ) ) + ( B x. R ) ) + C ) $=
          ( cmul co csm csp caddc cmv ccj cneg c2 cexp normlem1 normlem3 eqtr4i
          cfv ) FEDPQGRQUAQZUJSQFFSQEUBUIDUCZPQFGSQPQTQEUKPQGFSQPQDUDUEQZGGSQPQ
          TQTQAULPQBDPQTQCTQDEFGHIJNOUFABCDEFGHIJKLMNUGUH $.
          $( [29-Jul-1999] $)

        $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
           [Beran] p. 97. $)
        normlem5 $p |- 0 <_
                       ( ( ( A x. ( R ^ 2 ) ) + ( B x. R ) ) + C ) $=
          ( cc0 cmul co caddc cle csm cmv csp cexp chil wcel wbr recni hvmulcli
          c2 mulcli hvsubcli hiidge0 ax-mp normlem4 breqtri ) PFEDQRZGUARZUBRZU
          SUCRZADUJUDRQRBDQRSRCSRTUSUEUFPUTTUGFURIUQGEDHDNUHUKJUIULUSUMUNABCDEF
          GHIJKLMNOUOUP $.
          $( [10-Aug-1999] $)
      $}

      $d x A $.  $d x B $.  $d x C $.
      normlem6.7 $e |- ( abs ` S ) = 1 $.
      $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
         [Beran] p. 97. $)
      normlem6 $p |- ( abs ` B ) <_
                       ( 2 x. ( ( sqr ` A ) x. ( sqr ` C ) ) ) $=
        ( co cmul cle wbr cc0 cr wcel vx c2 cexp csqr cfv c4 cabs cmin wtru csp
        caddc chil hiidrcl ax-mp eqeltri a1i normlem2 cv cif oveq1 oveq2d oveq2
        wceq oveq12d oveq1d breq2d 0re elimel normlem5 dedth discr trud resqcli
        adantl 4re remulcli lesubadd2i mpbi recni addid1i breqtri sqge0i ltleii
        4pos hiidge0 breqtrri mulge0i mp2an sqrlei absrei sqrmulii sqr4 oveq12i
        wb eqtr2i 3brtr4i ) BUBUCNZUDUEZUFACONZONZUDUEZBUGUEUBAUDUECUDUEONZONZP
        WQWTPQZWRXAPQZWQWTRUKNZWTPWQWTUHNRPQZWQXFPQXGUIUAABCASTUIAFFUJNZSKFULTZ
        XHSTIFUMUNUOZUPBSTUIBDEFGHIJUQZUPCSTUICEEUJNZSLEULTZXLSTHEUMUNUOZUPUAUR
        ZSTZRAXOUBUCNZONZBXOONZUKNZCUKNZPQZUIXPYBRAXPXORUSZUBUCNZONZBYCONZUKNZC
        UKNZPQXORXOYCVCZYAYHRPYIXTYGCUKYIXRYEXSYFUKYIXQYDAOXOYCUBUCUTVAXOYCBOVB
        VDVEVFABCYCDEFGHIJKLXORSVGVHMVIVJVNVKVLWQWTRBXKVMZUFWSVOACXJXNVPZVPZVGV
        QVRWTWTYLVSVTWARWQPQRWTPQZXDXEWNBXKWBRUFPQRWSPQZYMRUFVGVOWDWCZRAPQRCPQY
        NRXHAPXIRXHPQIFWEUNKWFZRXLCPXMRXLPQHEWEUNLWFZACXJXNWGWHZUFWSVOYKWGWHWQW
        TYJYLWIWHVRBXKWJXAUFUDUEZWSUDUEZONXCUFWSVOYKYOYRWKYSUBYTXBOWLACXJXNYPYQ
        WKWMWOWP $.
        $( [4-Jun-2014] $) $( [2-Aug-1999] $)
    $}
    normlem7.4 $e |- ( abs ` S ) = 1 $.
    $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
       [Beran] p. 97. $)
    normlem7 $p |- ( ( ( * ` S ) x. ( F .ih G ) ) + ( S x. ( G .ih F ) ) ) <_
                ( 2 x. ( ( sqr ` ( G .ih G ) ) x. ( sqr ` ( F .ih F ) ) ) ) $=
      ( cfv csp co cmul cabs cle wbr c2 csqr cr wcel eqid hicli ccj caddc cjcli
      cneg normlem2 mulcli addcli negrebi mpbi leabsi absnegi breqtrri normlem6
      negcli abscli 2re chil cc0 hiidge0 ax-mp sqrcli mp2b remulcli letri mp2an
      hiidrcl ) AUAHZBCIJZKJZACBIJZKJZUBJZVLUDZLHZMNVNOCCIJZPHZBBIJZPHZKJZKJZMN
      VLVTMNVLVLLHVNMVLVMQRVLQRVMABCDEFVMSZUEVLVIVKVGVHADUCBCEFTUFAVJDCBFETUFUG
      ZUHUIZUJVLWBUKULVOVMVQABCDEFWAVOSVQSGUMVLVNVTWCVMVLWBUNUOOVSUPVPVRCUQRZUR
      VOMNVPQRFCUSVOWDVOQRFCVFUTVAVBBUQRZURVQMNVRQREBUSVQWEVQQREBVFUTVAVBVCVCVD
      VE $.
      $( [11-Aug-1999] $)
  $}

  ${
    normlem8.1 $e |- A e. ~H $.
    normlem8.2 $e |- B e. ~H $.
    normlem8.3 $e |- C e. ~H $.
    normlem8.4 $e |- D e. ~H $.
    $( Lemma used to derive properties of norm. $)
    normlem8 $p |- ( ( A +h B ) .ih ( C +h D ) ) =
        ( ( ( A .ih C ) + ( B .ih D ) ) + ( ( A .ih D ) + ( B .ih C ) ) ) $=
      ( cva co csp caddc chil wcel wceq his7 mp3an oveq12i hvaddcli hicli
      ax-his2 add42i 3eqtr4i ) ACDIJZKJZBUDKJZLJZACKJZADKJZLJZBCKJZBDKJZLJZLJAB
      IJUDKJZUHULLJUIUKLJLJUEUJUFUMLAMNZCMNZDMNZUEUJOEGHACDPQBMNZUPUQUFUMOFGHBC
      DPQRUOURUDMNUNUGOEFCDGHSABUDUAQUHULUIUKACEGTBDFHTADEHTBCFGTUBUC $.
      $( [30-Jun-2005] $)

    $( Lemma used to derive properties of norm. $)
    normlem9 $p |- ( ( A -h B ) .ih ( C -h D ) ) =
        ( ( ( A .ih C ) + ( B .ih D ) ) - ( ( A .ih D ) + ( B .ih C ) ) ) $=
      ( co csp c1 cneg caddc ax-1cn cmul wcel chil wceq mp3an 3eqtri cmv negcli
      csm cva cmin hvsubvali oveq12i hvmulcli normlem8 ccj cc ax-his3 oveq2i cr
      cfv his5 1re renegcli ax-mp mul2negi mulid2i oveq1i cjcli mulassi 3eqtr3i
      cjre hicli mulm1i eqtri negdii eqtr4i addcli negsubi ) ABUAIZCDUAIZJIAKLZ
      BUCIZUDIZCVPDUCIZUDIZJIACJIZVQVSJIZMIZAVSJIZVQCJIZMIZMIZWABDJIZMIZADJIZBC
      JIZMIZUEIZVNVRVOVTJABEFUFCDGHUFUGAVQCVSEVPBKNUBZFUHGVPDWNHUHZUIWGWIWLLZMI
      WMWCWIWFWPMWBWHWAMWBVPBVSJIZOIZVPVPUJUOZWHOIZOIZWHVPUKPZBQPZVSQPWBWRRWNFW
      OVPBVSULSWQWTVPOXBXCDQPZWQWTRWNFHVPBDUPSUMVPWSOIZWHOIKWHOIXAWHXEKWHOXEVPV
      POIKKOIKWSVPVPOVPUNPWSVPRKUQURVPVFUSZUMKKNNUTKNVATVBVPWSWHWNVPWNVCBDFHVGZ
      VDWHXGVAVETUMWFWJLZWKLZMIWPWDXHWEXIMWDWSWJOIZVPWJOIXHXBAQPXDWDXJRWNEHVPAD
      UPSWSVPWJOXFVBWJADEHVGZVHTWEVPWKOIZXIXBXCCQPWEXLRWNFGVPBCULSWKBCFGVGZVHVI
      UGWJWKXKXMVJVKUGWIWLWAWHACEGVGXGVLWJWKXKXMVLVMVIT $.
      $( [30-Jun-2005] $)
  $}

  ${
    normlem7t.1 $e |- A e. ~H $.
    normlem7t.2 $e |- B e. ~H $.
    $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
       [Beran] p. 97. $)
    normlem7tALT $p |- ( ( S e. CC /\ ( abs ` S ) = 1 ) ->
              ( ( ( * ` S ) x. ( A .ih B ) ) + ( S x. ( B .ih A ) ) ) <_
              ( 2 x. ( ( sqr ` ( B .ih B ) ) x. ( sqr ` ( A .ih A ) ) ) ) ) $=
      ( cc wcel cabs cfv c1 wceq wa ccj csp co cmul caddc cle wbr fveq2 c2 csqr
      cif oveq1d oveq1 oveq12d breq1d eqeq1d anbi12d ax-1cn cc0 0re lt01 ltleii
      eleq1 1re absidi ax-mp pm3.2i elimhyp simpli simpri normlem7 dedth ) CFGZ
      CHIZJKZLZCMIZABNOZPOZCBANOZPOZQOZUABBNOUBIAANOUBIPOPOZRSVHCJUCZMIZVJPOZVP
      VLPOZQOZVORSCJCVPKZVNVTVORWAVKVRVMVSQWAVIVQVJPCVPMTUDCVPVLPUEUFUGVPABVPFG
      ZVPHIZJKZVHWBWDLJFGZJHIZJKZLCJWAVEWBVGWDCVPFUOWAVFWCJCVPHTUHUIJVPKZWEWBWG
      WDJVPFUOWHWFWCJJVPHTUHUIWEWGUJUKJRSWGUKJULUPUMUNJUPUQURUSUTZVADEWBWDWIVBV
      CVD $.
      $( [11-Oct-1999] $)

    $( Equality case of Bunjakovaskij-Cauchy-Schwarz inequality.  Specifically,
       in the equality case the two vectors are collinear.  Compare
       ~ bcsiHIL . $)
    bcseqi $p |- ( ( ( A .ih B ) x. ( B .ih A ) ) = ( ( A .ih A ) x.
             ( B .ih B ) ) <-> ( ( B .ih B ) .h A ) = ( ( A .ih B ) .h B ) ) $=
      ( csp co cmul wceq cc0 caddc hicli wcel chil mp3an ax-his3 oveq12i mulcli
      his5 mulcomi 3eqtri csm cmv c0v hvmulcli normlem9 oveq1 eqcomd ccj cfv cc
      cmin hiidrcl cjre mp2b oveq1i eqtr4i his1i eqcomi mulassi mul32i 3eqtr4ri
      cr 3eqtr4g oveq12d oveq1d addcli subidi syl6eq syl5eq hvsubcli his6 ax-mp
      a1i wb sylib hvsubeq0i impbii ) ABEFZBAEFZGFZAAEFZBBEFZGFZHZWBAUAFZVRBUAF
      ZHZWDWEWFUBFZUCHZWGWDWHWHEFZIHZWIWDWJWEWEEFZWFWFEFZJFZWEWFEFZWFWEEFZJFZUK
      FZIWEWFWEWFWBABBDDKZCUDZVRBABCDKZDUDZWTXBUEWDWRWQWQUKFIWDWNWQWQUKWDWLWOWM
      WPJWDWCWBGFZVTWBGFZWLWOWDXDXCVTWCWBGUFUGWLWBUHUIZWEAEFZGFZWBWBWAGFZGFZXCW
      BUJLZWEMLZAMLZWLXGHWSWTCWBWEARNXEWBXFXHGBMLZWBVBLXEWBHDBULWBUMUNZXJXLXLXF
      XHHWSCCWBAAONZPXIXHWBGFXCWBXHWSWBWAWSAACCKZQSWCXHWBGWAWBXPWSSZUOUPTWOVRUH
      UIZWEBEFZGFZVSWBVRGFZGFZXDVRUJLZXKXMWOXTHXAWTDVRWEBRNXRVSXSYAGVSXRBADCUQU
      RZXJXLXMXSYAHWSCDWBABONPYBYAVSGFWBVTGFZXDVSYABADCKZWBVRWSXAQSWBVRVSWSXAYF
      USWBVTWSVRVSXAYFQZSTTVCWMWPHWDXEWFAEFZGFZYEWPWMXEWBYHVTGXNYCXMXLYHVTHXADC
      VRBAONZPXJWFMLZXLWPYIHWSXBCWBWFARNWMXRWFBEFZGFZVSVRWBGFZGFZYEYCYKXMWMYMHX
      AXBDVRWFBRNXRVSYLYNGYDYCXMXMYLYNHXADDVRBBONPYOYNVSGFXDYEVSYNYFVRWBXAWSQSV
      RWBVSXAWSYFUTVTWBYGWSSTTVAVMVDVEWQWOWPWEWFWTXBKWFWEXBWTKVFVGVHVIWHMLWKWIV
      NWEWFWTXBVJWHVKVLVOWEWFWTXBVPVOWGWCVTWGXFYHWCVTWEWFAEUFWCXHXFXQXOUPYHVTYJ
      URVCUGVQ $.
      $( [16-Jul-2001] $)
  $}

  $( Lemma used to derive properties of norm.  Part of Remark 3.4(B) of [Beran]
     p. 98. $)
  normlem9at $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( A -h B ) .ih ( A -h B ) ) =
      ( ( ( A .ih A ) + ( B .ih B ) ) - ( ( A .ih B ) + ( B .ih A ) ) ) ) $=
    ( chil wcel cmv co csp caddc cmin wceq c0v cif oveq1 oveq12d oveq1d eqeq12d
    id oveq2 ax-hv0cl elimel oveq2d normlem9 dedth2h ) ACDZBCDZABEFZUFGFZAAGFZB
    BGFZHFZABGFZBAGFZHFZIFZJUDAKLZBEFZUPGFZUOUOGFZUIHFZUOBGFZBUOGFZHFZIFZJUOUEB
    KLZEFZVEGFZURVDVDGFZHFZUOVDGFZVDUOGFZHFZIFZJABKKAUOJZUGUQUNVCVMUFUPUFUPGAUO
    BEMZVNNVMUJUSUMVBIVMUHURUIHVMAUOAUOGVMQZVONOVMUKUTULVAHAUOBGMAUOBGRNNPBVDJZ
    UQVFVCVLVPUPVEUPVEGBVDUOERZVQNVPUSVHVBVKIVPUIVGURHVPBVDBVDGVPQZVRNUAVPUTVIV
    AVJHBVDUOGRBVDUOGMNNPUOVDUOVDAKCSTZBKCSTZVSVTUBUC $.
    $( [10-May-2005] $)

