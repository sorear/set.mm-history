$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Norms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Alternate definition of the norm of a vector of Hilbert space.
       Definition of norm in [Beran] p. 96.  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    dfhnorm2 $p |- normh = ( x e. ~H |-> ( sqr ` ( x .ih x ) ) ) $=
      ( cno csp cdm cv co csqr cfv cmpt chil df-hnorm cxp cc ax-hfi fdmi dmxpid
      dmeqi eqtr2i eqid mpteq12i eqtr4i ) BACDZDZAEZUDCFGHZIAJUEIAKAJUEUCUEUCJJ
      LZDJUBUFUFMCNOQJPRUESTUA $.
      $( [15-Dec-2013] $) $( [6-Jun-2008] $)

    $( The norm function maps from Hilbert space to reals. $)
    normf $p |- normh : ~H --> RR $=
      ( vx cv csp co csqr cfv cr wcel chil wral cno cc0 cle wbr hiidrcl hiidge0
      wf resqrcl syl2anc rgen dfhnorm2 fmpt mpbi ) ABZUDCDZEFZGHZAIJIGKQUGAIUDI
      HUEGHLUEMNUGUDOUDPUERSTAIGUFKAUAUBUC $.
      $( [15-Dec-2013] $) $( [6-Sep-2007] $)
  $}

  ${
    $d x A $.
    $( The value of the norm of a vector in Hilbert space.  Definition of norm
       in [Beran] p. 96.  In the literature, the norm of ` A ` is usually
       written as "|| ` A ` ||", but we use function value notation to take
       advantage of our existing theorems about functions. $)
    normval $p |- ( A e. ~H -> ( normh ` A ) = ( sqr ` ( A .ih A ) ) ) $=
      ( vx cv csp co csqr cfv chil cno wceq oveq12 anidms fveq2d dfhnorm2 fvmpt
      fvex ) BABCZQDEZFGAADEZFGHIQAJZRSFTRSJQAQADKLMBNSFPO $.
      $( [23-Dec-2013] $) $( [29-May-1999] $)
  $}

  $( Real closure of the norm of a vector. $)
  normcl $p |- ( A e. ~H -> ( normh ` A ) e. RR ) $=
    ( chil cr cno normf ffvelrni ) BCADEF $.
    $( [29-May-1999] $)

  $( The norm of a vector is nonnegative. $)
  normge0 $p |- ( A e. ~H -> 0 <_ ( normh ` A ) ) $=
    ( chil wcel cc0 csp co csqr cfv cno cle wbr hiidrcl hiidge0 syl2anc normval
    cr sqrge0 breqtrrd ) ABCZDAAEFZGHZAIHJSTPCDTJKDUAJKALAMTQNAOR $.
    $( [29-May-1999] $)

  $( The norm of nonzero vector is positive. $)
  normgt0 $p |- ( A e. ~H -> ( A =/= 0h <-> 0 < ( normh ` A ) ) ) $=
    ( chil wcel c0v wne cc0 csp co csqr cfv clt wbr cno wa hiidrcl syl2anc wceq
    cr ex wn adantr ax-his4 sqrgt0 hi01 sylan9eqr fveq2d sqr0 syl6eq wb hiidge0
    oveq1 cle resqrcl lttri3 sylancl syl6bi syld necon2ad impbid normval breq2d
    0re simpr bitr4d ) ABCZADEZFAAGHZIJZKLZFAMJZKLVEVFVIVEVFVIVEVFNVGRCZFVGKLVI
    VEVKVFAOZUAAUBVGUCPSVEVIADVEADQZVHFQZVITZVEVMVNVEVMNZVHFIJFVPVGFIVMVEVGDAGH
    FADAGUKAUDUEUFUGUHSVEVNVHFKLTZVONZVOVEVHRCZFRCVNVRUIVEVKFVGULLVSVLAUJVGUMPV
    BVHFUNUOVQVOVCUPUQURUSVEVJVHFKAUTVAVD $.
    $( [10-Apr-2006] $)

  $( The norm of a zero vector. $)
  norm0 $p |- ( normh ` 0h ) = 0 $=
    ( c0v cno cfv csp co csqr chil wcel wceq ax-hv0cl normval ax-mp hi01 fveq2d
    cc0 sqr0 3eqtri ) ABCZAADEZFCZOFCZOAGHZRTIJAKLUBTUAIJUBSOFAMNLPQ $.
    $( [30-May-1999] $)

  $( Theorem 3.3(i) of [Beran] p. 97. $)
  norm-i $p |- ( A e. ~H -> ( ( normh ` A ) = 0 <-> A = 0h ) ) $=
    ( chil wcel c0v wceq cno cfv cc0 wne clt wbr normgt0 cle normcl normge0 0re
    cr wb leltne mp3an1 syl2anc bitrd necon4bid bicomd ) ABCZADEAFGZHEUEADUFHUE
    ADIHUFJKZUFHIZALUEUFQCZHUFMKZUGUHRZANAOHQCUIUJUKPHUFSTUAUBUCUD $.
    $( [29-Jul-1999] $)

  $( A norm is nonzero iff its argument is a nonzero vector. $)
  normne0 $p |- ( A e. ~H -> ( ( normh ` A ) =/= 0 <-> A =/= 0h ) ) $=
    ( chil wcel cno cfv cc0 c0v norm-i necon3bid ) ABCADEFAGAHI $.
    $( [11-Mar-2006] $)

  ${
    normcl.1 $e |- A e. ~H $.
    $( Real closure of the norm of a vector. $)
    normcli $p |- ( normh ` A ) e. RR $=
      ( chil wcel cno cfv cr normcl ax-mp ) ACDAEFGDBAHI $.
      $( [30-Sep-1999] $)

    $( The square of a norm. $)
    normsqi $p |- ( ( normh ` A ) ^ 2 ) = ( A .ih A ) $=
      ( cno cfv c2 cexp co csp csqr chil wcel wceq normval ax-mp oveq1i cc0 cle
      wbr hiidge0 cr hiidrcl sqsqri eqtri ) ACDZEFGAAHGZIDZEFGZUEUDUFEFAJKZUDUF
      LBAMNOPUEQRZUGUELUHUIBASNUEUHUETKBAUANUBNUC $.
      $( [21-Aug-1999] $)

    $( Theorem 3.3(i) of [Beran] p. 97. $)
    norm-i-i $p |- ( ( normh ` A ) = 0 <-> A = 0h ) $=
      ( chil wcel cno cfv cc0 wceq c0v wb norm-i ax-mp ) ACDAEFGHAIHJBAKL $.
      $( [5-Sep-1999] $)
  $}

  $( The square of a norm. $)
  normsq $p |- ( A e. ~H -> ( ( normh ` A ) ^ 2 ) = ( A .ih A ) ) $=
    ( chil wcel cno cfv c2 cexp co csp wceq c0v fveq2 oveq1d id oveq12d eqeq12d
    cif ax-hv0cl elimel normsqi dedth ) ABCZADEZFGHZAAIHZJUBAKQZDEZFGHZUFUFIHZJ
    AKAUFJZUDUHUEUIUJUCUGFGAUFDLMUJAUFAUFIUJNZUKOPUFAKBRSTUA $.
    $( [12-May-2005] $)

  ${
    normsub0.1 $e |- A e. ~H $.
    normsub0.2 $e |- B e. ~H $.
    $( Two vectors are equal iff the norm of their difference is zero. $)
    normsub0i $p |- ( ( normh ` ( A -h B ) ) = 0 <-> A = B ) $=
      ( cmv co cno cfv cc0 wceq c0v hvsubcli norm-i-i hvsubeq0i bitri ) ABEFZGH
      IJPKJABJPABCDLMABCDNO $.
      $( [18-Aug-1999] $)
  $}

  $( Two vectors are equal iff the norm of their difference is zero. $)
  normsub0 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( normh ` ( A -h B ) ) = 0 <-> A = B ) ) $=
    ( chil wcel cmv co cno cfv cc0 wb c0v cif oveq1 fveq2d eqeq1d eqeq1 bibi12d
    wceq ax-hv0cl elimel oveq2 eqeq2 normsub0i dedth2h ) ACDZBCDZABEFZGHZIRZABR
    ZJUEAKLZBEFZGHZIRZUKBRZJUKUFBKLZEFZGHZIRZUKUPRZJABKKAUKRZUIUNUJUOVAUHUMIVAU
    GULGAUKBEMNOAUKBPQBUPRZUNUSUOUTVBUMURIVBULUQGBUPUKEUANOBUPUKUBQUKUPAKCSTBKC
    STUCUD $.
    $( [18-Aug-1999] $)

  ${
    norm-ii.1 $e |- A e. ~H $.
    norm-ii.2 $e |- B e. ~H $.
    $( Triangle inequality for norms.  Theorem 3.3(ii) of [Beran] p. 97. $)
    norm-ii-i $p |- ( normh ` ( A +h B ) ) <_
                  ( ( normh ` A ) + ( normh ` B ) ) $=
      ( co csp csqr cfv caddc cle c2 wbr cmul c1 cr wcel wceq ax-1cn cc0 ax-mp
      cva cno cexp ccj cjrebi mpbi oveq1i hicli mulid2i eqtri oveq12i cabs lt01
      1re 0re ltleii absidi normlem7 eqbrtrri cneg normlem2 cjcli mulcli addcli
      eqid negrebi eqeltrri 2re chil hiidge0 hiidrcl remulcli readdcli normlem8
      sqrcli leadd2i addcomi oveq2i recni binom2i sqcli add32i sqsqri 3eqtri wb
      2cn 3brtr4i hvaddcli sqge0i resqcli sqrlei sqrge0i addge0i sqrsqi breqtri
      mp2an normval ) ABUAEZWRFEZGHZAAFEZGHZBBFEZGHZIEZWRUBHZAUBHZBUBHZIEJWTXEK
      UCEZGHZXEJWSXIJLZWTXJJLZXAXCIEZBAFEZABFEZIEZIEZXMKXBXDMEZMEZIEZWSXIJXPXSJ
      LXQXTJLNUDHZXNMEZNXOMEZIEZXPXSJYBXNYCXOIYBNXNMEXNYANXNMNOPYANQUNNRUEUFUGX
      NBADCUHZUIUJXOABCDUHZUIUKZNBARDCSNJLNULHNQSNUOUNUMUPNUNUQTURUSXPXSXMYDXPO
      YGYDUTZOPYDOPYHNBARDCYHVEVAYDYBYCYAXNNRVBYEVCNXORYFVCVDVFUFVGKXRVHXBXDSXA
      JLZXBOPAVIPZYICAVJTZXAYJXAOPCAVKTZVOTZSXCJLZXDOPBVIPZYNDBVJTZXCYOXCOPDBVK
      TZVOTZVLVLXAXCYLYQVMVPUFWSXMXOXNIEZIEXQABABCDCDVNYSXPXMIXOXNYFYEVQVRUJXIX
      BKUCEZXSIEXDKUCEZIEYTUUAIEZXSIEXTXBXDXBYMVSZXDYRVSZVTYTXSUUAXBUUCWAKXRWFX
      BXDUUCUUDVCVCXDUUDWAWBUUBXMXSIYTXAUUAXCIYIYTXAQYKXAYLWCTYNUUAXCQYPXCYQWCT
      UKUGWDWGSWSJLZSXIJLXKXLWEWRVIPZUUEABCDWHZWRVJTXEXBXDYMYRVMZWIWSXIUUFWSOPU
      UGWRVKTXEUUHWJWKWPUFSXEJLZXJXEQSXBJLZSXDJLZUUIYIUUJYKXAYLWLTYNUUKYPXCYQWL
      TXBXDYMYRWMWPXEUUHWNTWOUUFXFWTQUUGWRWQTXGXBXHXDIYJXGXBQCAWQTYOXHXDQDBWQTU
      KWG $.
      $( [11-Aug-1999] $)
  $}

  $( Triangle inequality for norms.  Theorem 3.3(ii) of [Beran] p. 97. $)
  norm-ii $p |- ( ( A e. ~H /\ B e. ~H ) ->
               ( normh ` ( A +h B ) ) <_ ( ( normh ` A ) + ( normh ` B ) ) ) $=
    ( chil wcel cva co cno cfv caddc cle wbr c0v cif oveq1 fveq2d fveq2 breq12d
    wceq ax-hv0cl elimel oveq1d oveq2 oveq2d norm-ii-i dedth2h ) ACDZBCDZABEFZG
    HZAGHZBGHZIFZJKUFALMZBEFZGHZUMGHZUKIFZJKUMUGBLMZEFZGHZUPURGHZIFZJKABLLAUMRZ
    UIUOULUQJVCUHUNGAUMBENOVCUJUPUKIAUMGPUAQBURRZUOUTUQVBJVDUNUSGBURUMEUBOVDUKV
    AUPIBURGPUCQUMURALCSTBLCSTUDUE $.
    $( [10-Mar-2006] $)

  ${
    norm-iii.1 $e |- A e. CC $.
    norm-iii.2 $e |- B e. ~H $.
    $( Theorem 3.3(iii) of [Beran] p. 97. $)
    norm-iii-i $p |- ( normh ` ( A .h B ) )
         = ( ( abs ` A ) x. ( normh ` B ) ) $=
      ( csm co csp csqr cfv ccj cmul cabs his35i fveq2i chil wcel ax-mp normval
      cno wceq cjmulrcli cr hiidrcl cjmulge0i cc0 cle hiidge0 sqrmulii hvmulcli
      wbr eqtri cc absval oveq12i 3eqtr4i ) ABEFZUPGFZHIZAAJIKFZHIZBBGFZHIZKFZU
      PSIZALIZBSIZKFURUSVAKFZHIVCUQVGHAABBCCDDMNUSVAACUABOPZVAUBPDBUCQACUDVHUEV
      AUFUJDBUGQUHUKUPOPVDURTABCDUIUPRQVEUTVFVBKAULPVEUTTCAUMQVHVFVBTDBRQUNUO
      $.
      $( [29-Jul-1999] $)
  $}

  $( Theorem 3.3(iii) of [Beran] p. 97. $)
  norm-iii $p |- ( ( A e. CC /\ B e. ~H )
             -> ( normh ` ( A .h B ) ) = ( ( abs ` A ) x. ( normh ` B ) ) ) $=
    ( cc wcel chil csm co cno cfv cabs cmul wceq cc0 cif c0v oveq1 fveq2d fveq2
    eqeq12d elimel oveq1d oveq2 oveq2d 0cn ax-hv0cl norm-iii-i dedth2h ) ACDZBE
    DZABFGZHIZAJIZBHIZKGZLUHAMNZBFGZHIZUOJIZUMKGZLUOUIBONZFGZHIZURUTHIZKGZLABMO
    AUOLZUKUQUNUSVEUJUPHAUOBFPQVEULURUMKAUOJRUASBUTLZUQVBUSVDVFUPVAHBUTUOFUBQVF
    UMVCURKBUTHRUCSUOUTAMCUDTBOEUETUFUG $.
    $( [25-Oct-1999] $)

  ${
    normsub.1 $e |- A e. ~H $.
    normsub.2 $e |- B e. ~H $.
    $( Negative doesn't change the norm of a Hilbert space vector. $)
    normsubi $p |- ( normh ` ( A -h B ) ) = ( normh ` ( B -h A ) ) $=
      ( cmv co cno cfv c1 cneg cabs cmul hvnegdii fveq2i ax-1cn negcli hvsubcli
      csm cc0 1re norm-iii-i eqtr3i absnegi cle wbr wceq 0re lt01 ltleii absidi
      ax-mp eqtri oveq1i normcli recni mulid2i 3eqtri ) ABEFZGHZIJZKHZBAEFZGHZL
      FZIVCLFVCUTVBRFZGHUSVDVEURGBADCMNUTVBIOPBADCQZUAUBVAIVCLVAIKHZIIOUCSIUDUE
      VGIUFSIUGTUHUIITUJUKULUMVCVCVBVFUNUOUPUQ $.
      $( [11-Aug-1999] $)

    $( Analogy to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
       [Beran] p. 98. $)
    normpythi $p |- ( ( A .ih B ) = 0 -> ( ( normh ` ( A +h B ) ) ^ 2 ) =
                   ( ( ( normh ` A ) ^ 2 ) + ( ( normh ` B ) ^ 2 ) ) ) $=
      ( csp co cc0 wceq cva caddc cno c2 cexp normlem8 chil wcel syl6eq normsqi
      cfv hicli id wb orthcom mp2an biimpi oveq12d oveq2d addcli addid1i syl5eq
      00id hvaddcli oveq12i 3eqtr4g ) ABEFZGHZABIFZUQEFZAAEFZBBEFZJFZUQKSLMFAKS
      LMFZBKSLMFZJFUPURVAUOBAEFZJFZJFZVAABABCDCDNUPVFVAGJFVAUPVEGVAJUPVEGGJFGUP
      UOGVDGJUPUAUPVDGHZAOPBOPUPVGUBCDABUCUDUEUFUKQUGVAUSUTAACCTBBDDTUHUIQUJUQA
      BCDULRVBUSVCUTJACRBDRUMUN $.
      $( [17-Oct-1999] $)
  $}

  $( Swapping order of subtraction doesn't change the norm of a vector. $)
  normsub $p |- ( ( A e. ~H /\ B e. ~H ) ->
                 ( normh ` ( A -h B ) ) = ( normh ` ( B -h A ) ) ) $=
    ( chil wcel cmv co cno cfv wceq c0v cif oveq1 fveq2d oveq2 eqeq12d ax-hv0cl
    elimel normsubi dedth2h ) ACDZBCDZABEFZGHZBAEFZGHZITAJKZBEFZGHZBUFEFZGHZIUF
    UABJKZEFZGHZUKUFEFZGHZIABJJAUFIZUCUHUEUJUPUBUGGAUFBELMUPUDUIGAUFBENMOBUKIZU
    HUMUJUOUQUGULGBUKUFENMUQUIUNGBUKUFELMOUFUKAJCPQBJCPQRS $.
    $( [14-Aug-1999] $)

  $( The norm of a vector equals the norm of its negative. $)
  normneg $p |- ( A e. ~H ->
                 ( normh ` ( -u 1 .h A ) ) = ( normh ` A ) ) $=
    ( chil wcel c0v cmv co cno cfv cneg csm wceq ax-hv0cl normsub hv2neg fveq2d
    c1 mpan hvsub0 3eqtr3d ) ABCZDAEFZGHZADEFZGHZPIAJFZGHAGHDBCTUBUDKLDAMQTUAUE
    GANOTUCAGAROS $.
    $( [23-May-2005] $)

  $( Analogy to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
     [Beran] p. 98. $)
  normpyth $p |- ( ( A e. ~H /\ B e. ~H ) ->
                 ( ( A .ih B ) = 0 -> ( ( normh ` ( A +h B ) ) ^ 2 ) =
                 ( ( ( normh ` A ) ^ 2 ) + ( ( normh ` B ) ^ 2 ) ) ) ) $=
    ( chil wcel csp co cc0 wceq cva cno cfv c2 cexp caddc c0v cif eqeq1d oveq1d
    wi oveq1 fveq2d fveq2 eqeq12d imbi12d oveq2 oveq2d elimel normpythi dedth2h
    ax-hv0cl ) ACDZBCDZABEFZGHZABIFZJKZLMFZAJKZLMFZBJKZLMFZNFZHZSUKAOPZBEFZGHZV
    DBIFZJKZLMFZVDJKZLMFZVANFZHZSVDULBOPZEFZGHZVDVNIFZJKZLMFZVKVNJKZLMFZNFZHZSA
    BOOAVDHZUNVFVCVMWDUMVEGAVDBETQWDUQVIVBVLWDUPVHLMWDUOVGJAVDBITUARWDUSVKVANWD
    URVJLMAVDJUBRRUCUDBVNHZVFVPVMWCWEVEVOGBVNVDEUEQWEVIVSVLWBWEVHVRLMWEVGVQJBVN
    VDIUEUARWEVAWAVKNWEUTVTLMBVNJUBRUFUCUDVDVNAOCUJUGBOCUJUGUHUI $.
    $( [17-Oct-1999] $)

  $( Corollary to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
     [Beran] p. 98. $)
  normpyc $p |- ( ( A e. ~H /\ B e. ~H ) ->
            ( ( A .ih B ) = 0 -> ( normh ` A ) <_ ( normh ` ( A +h B ) ) ) ) $=
    ( chil wcel wa co cc0 wceq cno cfv c2 cle wbr caddc cr normcl resqcl adantr
    cexp syl csp cva cc recnd addid1 sqge0 adantl wb 0re leadd2 mp3an1 eqbrtrrd
    syl2anr mpbid normpyth imp breqtrrd normge0 hvaddcl le2sq syl22anc sylibrd
    ex ) ACDZBCDZEZABUAFGHZAIJZKSFZABUBFZIJZKSFZLMZVHVKLMZVFVGVMVFVGEVIVIBIJZKS
    FZNFZVLLVFVIVQLMVGVFVIGNFZVIVQLVDVRVIHZVEVDVIUCDVSVDVIVDVHODZVIODZAPZVHQTZU
    DVIUETRVFGVPLMZVRVQLMZVEWDVDVEVOODZWDBPZVOUFTUGVEVPODZWAWDWEUHZVDVEWFWHWGVO
    QTWCGODWHWAWIUIGVPVIUJUKUMUNULRVFVGVLVQHABUOUPUQVCVFVTGVHLMZVKODZGVKLMZVNVM
    UHVDVTVEWBRVDWJVEAURRVFVJCDZWKABUSZVJPTVFWMWLWNVJURTVHVKUTVAVB $.
    $( [26-Oct-1999] $)

  ${
    norm3dif.1 $e |- A e. ~H $.
    norm3dif.2 $e |- B e. ~H $.
    norm3dif.3 $e |- C e. ~H $.
    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3difi $p |- ( normh ` ( A -h B ) ) <_ ( ( normh ` ( A -h C ) ) +
                   ( normh ` ( C -h B ) ) ) $=
      ( cmv co cno cfv cva csm hvmulcli hvassi c0v hvcomi hvsubvali chil wcel
      c1 caddc cneg ax-1cn negcli hvaddcli wceq hvsubid 3eqtr2i oveq1i ax-hv0cl
      cle ax-mp ax-hvaddid 3eqtri eqtr3i oveq2i eqtr2i oveq12i 3eqtr4i hvsubcli
      fveq2i norm-ii-i eqbrtri ) ABGHZIJACGHZCBGHZKHZIJVEIJVFIJUAHUKVDVGIATUBZB
      LHZKHZAVHCLHZKHZCVIKHZKHZVDVGVNAVKVMKHZKHVJAVKVMDVHCTUCUDZFMZCVIFVHBVPEMZ
      UENVOVIAKVKCKHZVIKHZVOVIVKCVIVQFVRNVTOVIKHVIOKHZVIVSOVIKVSCVKKHCCGHZOVKCV
      QFPCCFFQCRSWBOUFFCUGULUHUIOVIUJVRPVIRSWAVIUFVRVIUMULUNUOUPUQABDEQVEVLVFVM
      KACDFQCBFEQURUSVAVEVFACDFUTCBFEUTVBVC $.
      $( [16-Aug-1999] $)

    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3adifii $p |- ( abs ` ( ( normh ` ( A -h C ) ) -
         ( normh ` ( B -h C ) ) ) ) <_ ( normh ` ( A -h B ) ) $=
      ( cmv co cno cfv cmin cle wbr cneg hvsubcli normcli recni caddc norm3difi
      lesubaddi cabs resubcli abslei negsubdi2i normsubi oveq1i breqtri eqbrtri
      mpbir lenegcon1i mpbi mpbir2an ) ACGHZIJZBCGHZIJZKHZUAJABGHZIJZLMUSNUQLMZ
      UQUSLMZUQUSUNUPUMACDFOPZUOBCEFOPZUBZURABDEOPZUCUQNZUSLMUTVFUPUNKHZUSLUNUP
      UNVBQUPVCQUDVGUSLMUPUSUNRHZLMUPBAGHIJZUNRHVHLBCAEFDSVIUSUNRBAEDUEUFUGUPUN
      USVCVBVETUIUHUQUSVDVEUJUKVAUNUSUPRHLMACBDFESUNUPUSVBVCVETUIUL $.
      $( [30-Sep-1999] $)

    ${
      norm3lem.4 $e |- D e. RR $.
      $( Lemma involving norm of differences in Hilbert space. $)
      norm3lem $p |- ( ( ( normh ` ( A -h C ) ) < ( D / 2 ) /\
                         ( normh ` ( C -h B ) ) < ( D / 2 ) ) ->
                         ( normh ` ( A -h B ) ) < D ) $=
        ( cmv co cno cfv c2 clt wbr caddc hvsubcli normcli 2ne0 readdcli wa cle
        cdiv norm3difi 2re redivcli lt2addi lelttri sylancr cmul recni divcan2i
        2timesi 2cn eqtr3i syl6breq ) ACIJZKLZDMUCJZNOCBIJZKLZUSNOUAZABIJZKLZUS
        USPJZDNVBVDURVAPJZUBOVFVENOVDVENOABCEFGUDURVAUSUSUQACEGQRZUTCBGFQRZDMHU
        ESUFZVIUGVDVFVEVCABEFQRURVAVGVHTUSUSVIVITUHUIMUSUJJVEDUSUSVIUKUMDMDHUKU
        NSULUOUP $.
        $( [18-Aug-1999] $)
    $}
  $}

  $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
     p. 101. $)
  norm3dif $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) -> ( normh ` ( A -h B ) )
                  <_ ( ( normh ` ( A -h C ) ) + ( normh ` ( C -h B ) ) ) ) $=
    ( chil wcel cmv co cno cfv caddc cle wbr c0v cif wceq oveq1 fveq2d ax-hv0cl
    oveq2 elimel oveq1d breq12d oveq2d oveq12d breq2d norm3difi dedth3h ) ADEZB
    DEZCDEZABFGZHIZACFGZHIZCBFGZHIZJGZKLUHAMNZBFGZHIZURCFGZHIZUPJGZKLURUIBMNZFG
    ZHIZVBCVDFGZHIZJGZKLVFURUJCMNZFGZHIZVJVDFGZHIZJGZKLABCMMMAUROZULUTUQVCKVPUK
    USHAURBFPQVPUNVBUPJVPUMVAHAURCFPQUAUBBVDOZUTVFVCVIKVQUSVEHBVDURFSQVQUPVHVBJ
    VQUOVGHBVDCFSQUCUBCVJOZVIVOVFKVRVBVLVHVNJVRVAVKHCVJURFSQVRVGVMHCVJVDFPQUDUE
    URVDVJAMDRTBMDRTCMDRTUFUG $.
    $( [20-Apr-2006] $)

  $( Norm of differences around common element. $)
  norm3dif2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) -> ( normh ` ( A -h B )
                  ) <_ ( ( normh ` ( C -h A ) ) + ( normh ` ( C -h B ) ) ) ) $=
    ( chil wcel w3a cmv cno cfv caddc cle norm3dif wceq normsub 3adant2 breqtrd
    co oveq1d ) ADEZBDEZCDEZFZABGQHIACGQHIZCBGQHIZJQCAGQHIZUDJQKABCLUBUCUEUDJSU
    AUCUEMTACNORP $.
    $( [18-Apr-2007] $)

  $( Lemma involving norm of differences in Hilbert space. $)
  norm3lemt $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\
                   D e. RR ) ) -> (
          ( ( normh ` ( A -h C ) ) < ( D / 2 ) /\
            ( normh ` ( C -h B ) ) < ( D / 2 ) ) ->
            ( normh ` ( A -h B ) ) < D ) ) $=
    ( chil wcel cmv co cno cfv c2 clt wbr wa wi c0v cif wceq fveq2d breq1d cdiv
    cr oveq1 anbi1d imbi12d oveq2 anbi2d anbi12d imbi1d breq2d breq2 elimel 2re
    ax-hv0cl norm3lem dedth4h ) AEFZBEFZCEFZDUBFZACGHZIJZDKUAHZLMZCBGHZIJZVCLMZ
    NZABGHZIJZDLMZOUQAPQZCGHZIJZVCLMZVGNZVLBGHZIJZDLMZOVOCURBPQZGHZIJZVCLMZNZVL
    VTGHZIJZDLMZOVLUSCPQZGHZIJZVCLMZWHVTGHZIJZVCLMZNZWGOWJUTDKQZKUAHZLMZWMWQLMZ
    NZWFWPLMZOABCDPPPKAVLRZVHVPVKVSXBVDVOVGXBVBVNVCLXBVAVMIAVLCGUCSTUDXBVJVRDLX
    BVIVQIAVLBGUCSTUEBVTRZVPWDVSWGXCVGWCVOXCVFWBVCLXCVEWAIBVTCGUFSTUGXCVRWFDLXC
    VQWEIBVTVLGUFSTUECWHRZWDWOWGXDVOWKWCWNXDVNWJVCLXDVMWIICWHVLGUFSTXDWBWMVCLXD
    WAWLICWHVTGUCSTUHUIDWPRZWOWTWGXAXEWKWRWNWSXEVCWQWJLDWPKUAUCZUJXEVCWQWMLXFUJ
    UHDWPWFLUKUEVLVTWHWPAPEUNULBPEUNULCPEUNULDKUBUMULUOUP $.
    $( [18-Aug-1999] $)

  ${
    norm3adift.1 $e |- C e. ~H $.
    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3adifi $p |- ( ( A e. ~H /\ B e. ~H ) ->
         ( abs ` ( ( normh ` ( A -h C ) ) -
         ( normh ` ( B -h C ) ) ) ) <_ ( normh ` ( A -h B ) ) ) $=
      ( chil wcel cmv co cno cfv cmin cabs cle wbr c0v cif oveq1 fveq2d breq12d
      wceq oveq1d oveq2d oveq2 ax-hv0cl elimel norm3adifii dedth2h ) AEFZBEFZAC
      GHZIJZBCGHZIJZKHZLJZABGHZIJZMNUHAOPZCGHZIJZUMKHZLJZURBGHZIJZMNUTUIBOPZCGH
      ZIJZKHZLJZURVEGHZIJZMNABOOAURTZUOVBUQVDMVLUNVALVLUKUTUMKVLUJUSIAURCGQRUAR
      VLUPVCIAURBGQRSBVETZVBVIVDVKMVMVAVHLVMUMVGUTKVMULVFIBVECGQRUBRVMVCVJIBVEU
      RGUCRSURVECAOEUDUEBOEUDUEDUFUG $.
      $( [3-Oct-1999] $)
  $}

  ${
    normpar.1 $e |- A e. ~H $.
    normpar.2 $e |- B e. ~H $.
    $( Parallelogram law for norms.  Remark 3.4(B) of [Beran] p. 98. $)
    normpari $p |- ( ( ( normh ` ( A -h B ) ) ^ 2 ) +
                    ( ( normh ` ( A +h B ) ) ^ 2 ) ) =
                  ( ( 2 x. ( ( normh ` A ) ^ 2 ) ) +
                    ( 2 x. ( ( normh ` B ) ^ 2 ) ) ) $=
      ( co csp caddc cno cfv c2 cexp hicli addcli oveq12i oveq2i 3eqtri normsqi
      cmul cc0 2timesi cmv cva cneg cmin normlem9 eqtr4i normlem8 negcli add42i
      negsubi negidi addid1i add4i hvsubcli hvaddcli eqtri 3eqtr4i ) ABUAEZURFE
      ZABUBEZUTFEZGEZAAFEZVCGEZBBFEZVEGEZGEZURHIJKEZUTHIJKEZGEJAHIJKEZREZJBHIJK
      EZREZGEVBVCVEGEZABFEZBAFEZGEZUCZGEZVNVQGEZGEVNVNGEZVQVRGEZGEZVGUSVSVAVTGU
      SVNVQUDEVSABABCDCDUEVNVQVCVEAACCLZBBDDLZMZVOVPABCDLBADCLMZUJUFABABCDCDUGN
      VNVRVNVQWFVQWGUHWFWGUIWCWASGEWAVGWBSWAGVQWGUKOWAVNVNWFWFMULVCVEVCVEWDWEWD
      WEUMPPVHUSVIVAGURABCDUNQUTABCDUOQNVKVDVMVFGVKJVCREVDVJVCJRACQOVCWDTUPVMJV
      EREVFVLVEJRBDQOVEWETUPNUQ $.
      $( [21-Aug-1999] $)
  $}

  $( Parallelogram law for norms.  Remark 3.4(B) of [Beran] p. 98. $)
  normpar $p |- ( ( A e. ~H /\ B e. ~H ) ->
          ( ( ( normh ` ( A -h B ) ) ^ 2 ) + ( ( normh ` ( A +h B ) ) ^ 2 ) ) =
       ( ( 2 x. ( ( normh ` A ) ^ 2 ) ) + ( 2 x. ( ( normh ` B ) ^ 2 ) ) ) ) $=
    ( chil wcel cmv co cno cfv c2 cexp cva caddc cmul wceq c0v cif oveq1 fveq2d
    oveq1d oveq2d oveq12d fveq2 eqeq12d oveq2 ax-hv0cl elimel normpari dedth2h
    ) ACDZBCDZABEFZGHZIJFZABKFZGHZIJFZLFZIAGHZIJFZMFZIBGHZIJFZMFZLFZNUIAOPZBEFZ
    GHZIJFZVEBKFZGHZIJFZLFZIVEGHZIJFZMFZVCLFZNVEUJBOPZEFZGHZIJFZVEVQKFZGHZIJFZL
    FZVOIVQGHZIJFZMFZLFZNABOOAVENZUQVLVDVPWIUMVHUPVKLWIULVGIJWIUKVFGAVEBEQRSWIU
    OVJIJWIUNVIGAVEBKQRSUAWIUTVOVCLWIUSVNIMWIURVMIJAVEGUBSTSUCBVQNZVLWDVPWHWJVH
    VTVKWCLWJVGVSIJWJVFVRGBVQVEEUDRSWJVJWBIJWJVIWAGBVQVEKUDRSUAWJVCWGVOLWJVBWFI
    MWJVAWEIJBVQGUBSTTUCVEVQAOCUEUFBOCUEUFUGUH $.
    $( [15-Apr-2007] $)

  ${
    normpar2.1 $e |- A e. ~H $.
    normpar2.2 $e |- B e. ~H $.
    normpar2.3 $e |- C e. ~H $.
    $( Corollary of parallelogram law for norms.  Part of Lemma 3.6 of [Beran]
       p. 100. $)
    normpar2i $p |- ( ( normh ` ( A -h B ) ) ^ 2 ) =
                   ( ( ( 2 x. ( ( normh ` ( A -h C ) ) ^ 2 ) ) +
                       ( 2 x. ( ( normh ` ( B -h C ) ) ^ 2 ) ) )
          - ( ( normh ` ( ( A +h B ) -h ( 2 .h C ) ) ) ^ 2 ) ) $=
      ( c2 cmv co cno cfv cexp cmul caddc cva recni 2cn c4 cdiv oveq1i csm cmin
      2re hvsubcli normcli resqcli remulcli readdcli hvaddcli hvmulcli 4re 2ne0
      mulcli divdiri addcomi cabs c1 cneg hvsubvali hvnegdii oveq12i hvsubcan2i
      hvcomi eqtri ax-1cn negcli hvadd32i 3eqtri hvsubdistr1i eqtr4i norm-iii-i
      3eqtr4i fveq2i cc0 cle wbr wceq 0re 2pos ltleii absidi ax-mp sq2 normpari
      sqmuli 3eqtr2i divcan3i div23i 4d2e2 3eqtr3i subaddrii eqcomi ) GACHIZJKZ
      GLIZMIZGBCHIZJKZGLIZMIZNIZABOIZGCUAIZHIZJKZGLIZUBIABHIZJKZGLIZXAXFXIXAWPW
      TGWOUCWNWMACDFUDZUEZUFZUGGWSUCWRWQBCEFUDZUEZUFZUGUHPXFXEXDXBXCABDEUIZGCQF
      UJZUDZUEUFPZXIXHXGABDEUDZUEUFPZRWOMIZRWSMIZNIZGSIZYBGSIZYCGSIZNIXFXINIZXA
      YBYCGRWORUKPZWOXLPZUMZRWSYIWSXOPZUMZQULUNYEGXFMIZGXIMIZNIZGSIYNGSIZYOGSIZ
      NIYHYDYPGSYDYCYBNIXDXGHIZJKZGLIZXDXGOIZJKZGLIZNIYPYBYCYKYMUOUUAYCUUDYBNUU
      AGWRMIZGLIGGLIZWSMIYCYTUUEGLYTGWQUAIZJKGUPKZWRMIUUEYSUUGJYSGBUAIZXCHIZUUG
      YSXDUQURZXGUAIZOIXBUUKXCUAIZOIZUULOIZUUJXDXGXRXTUSXDUUNUULOXBXCXPXQUSZTXB
      UULOIZUUMOIUUIUUMOIUUOUUJUUQUUIUUMOUUQBAOIZBAHIZOIUUIXBUURUULUUSOABDEVCAB
      DEUTVABAEDVBVDTXBUUMUULXPUUKXCUQVEVFZXQUJZUUKXGUUTXTUJVGUUIXCGBQEUJXQUSVL
      VHGBCQEFVIVJVMGWQQXMVKUUHGWRMVNGVOVPUUHGVQVNGVRUCVSVTGUCWAWBZTVHTGWRQWRXN
      PWEUUFRWSMWCTVHUUDGWNMIZGLIUUFWOMIYBUUCUVCGLUUCGWMUAIZJKUUHWNMIUVCUUBUVDJ
      UUBGAUAIZXCHIZUVDUUBUUNXGOIXBXGOIZUUMOIZUVFXDUUNXGOUUPTXBUUMXGXPUVAXTVGUV
      HUVEUUMOIUVFUVGUVEUUMOABDEVBTUVEXCGAQDUJXQUSVJVHGACQDFVIVJVMGWMQXJVKUUHGW
      NMUVBTVHTGWNQWNXKPWEUUFRWOMWCTVHVAXDXGXRXTWDWFTYNYOGGXFQXSUMGXIQYAUMQULUN
      YQXFYRXINXFGXSQULWGXIGYAQULWGVAVHYFWPYGWTNYFRGSIZWOMIWPRWOGYIYJQULWHUVIGW
      OMWITVDYGUVIWSMIWTRWSGYIYLQULWHUVIGWSMWITVDVAWJWKWL $.
      $( [5-Oct-1999] $)
  $}

  ${
    polid2.1 $e |- A e. ~H $.
    polid2.2 $e |- B e. ~H $.
    polid2.3 $e |- C e. ~H $.
    polid2.4 $e |- D e. ~H $.
    $( Generalized polarization identity.  Generalization of Exercise 4(a) of
       [ReedSimon] p. 63. $)
    polid2i $p |- ( A .ih B ) = ( ( ( ( ( A +h C ) .ih ( D +h B ) ) -
                ( ( A -h C ) .ih ( D -h B ) ) ) +
       ( _i x. ( ( ( A +h ( _i .h C ) ) .ih ( D +h ( _i .h B ) ) ) -
               ( ( A -h ( _i .h C ) ) .ih ( D -h ( _i .h B ) ) ) ) ) ) / 4 ) $=
      ( c4 csp co cmul cmin ci caddc hicli c2 addcli wcel ax-icn cdiv cva recni
      cmv csm 4re 4pos gt0ne0ii divcan3i 2cn subcli adddii cc wceq ppncan mp3an
      2timesi eqtr4i mulassi 3eqtr2ri pnncani normlem8 normlem9 oveq12i 3eqtr4i
      oveq2i 2t2e4 oveq1i cneg hvmulcli ccj cfv chil his5 ax-his3 eqtr3i 3eqtri
      cji eqtri negcli mulcli mul12i mulneg2i ixi negeqi ax-1cn negnegi mulid2i
      c1 3eqtr3i mulm1i negsubi 3eqtr2i ) IABJKZLKZIUAKWNACUBKDBUBKJKZACUDKDBUD
      KJKZMKZNANCUEKZUBKDNBUEKZUBKJKZAWSUDKDWTUDKJKZMKZLKZOKZIUAKWNIABEFPZIUFUC
      IUFUGUHUIWOXEIUAQWNCDJKZOKZWNXGMKZOKZLKZQXHLKZQXILKZOKWOXEQXHXIUJWNXGXFCD
      GHPZRZWNXGXFXNUKULXKQQWNLKZLKQQLKZWNLKWOXJXPQLXJWNWNOKZXPWNUMSZXGUMSXSXJX
      RUNXFXNXFWNXGWNUOUPWNXFUQURVFQQWNUJUJXFUSXQIWNLVGVHUTWRXLXDXMOADJKZCBJKZO
      KZXHOKZYBXHMKZMKXHXHOKWRXLYBXHXHXTYAADEHPZCBGFPRXOXOVAWPYCWQYDMACDBEGHFVB
      ACDBEGHFVCVDXHXOUQVEXDNQNVIZWNLKZNXGLKZOKZLKZLKQNYILKZLKXMXCYJNLXCXTWSWTJ
      KZOKZAWTJKZWSDJKZOKZOKZYMYPMKZMKYPYPOKZYJXAYQXBYRMAWSDWTENCTGVJZHNBTFVJZV
      BAWSDWTEYTHUUAVCVDYMYPYPXTYLYEWSWTYTUUAPRYNYOAWTEUUAPWSDYTHPRZUUBVAQYPLKY
      SYJYPUUBUQYPYIQLYNYGYOYHOYNNVKVLZWNLKZYGNUMSZAVMSBVMSYNUUDUNTEFNABVNUPUUC
      YFWNLVRVHVSUUECVMSDVMSYOYHUNTGHNCDVOUPVDVFVPVQVFQNYIUJTYGYHYFWNNTVTZXFWAZ
      NXGTXNWAZRWBYKXIQLYKNYGLKZNYHLKZOKWNXGVIZOKXINYGYHTUUGUUHULUUIWNUUJUUKONY
      FLKZWNLKWIWNLKUUIWNUULWIWNLUULNNLKZVIWIVIZVIWINNTTWCUUMUUNWDWEWIWFWGVQVHN
      YFWNTUUFXFUSWNXFWHWJUUMXGLKUUNXGLKUUJUUKUUMUUNXGLWDVHNNXGTTXNUSXGXNWKWJVD
      WNXGXFXNWLVQVFWMVDVEVHVP $.
      $( [30-Jun-2005] $)
  $}

  ${
    polid.1 $e |- A e. ~H $.
    polid.2 $e |- B e. ~H $.
    $( Polarization identity.  Recovers inner product from norm.  Exercise 4(a)
       of [ReedSimon] p. 63.  The outermost operation is + instead of - due to
       our mathematicians' (rather than physicists') version of axiom
       ~ ax-his3 . $)
    polidi $p |- ( A .ih B ) = ( ( ( ( ( normh ` ( A +h B ) ) ^ 2 ) -
                ( ( normh ` ( A -h B ) ) ^ 2 ) ) +
       ( _i x. ( ( ( normh ` ( A +h ( _i .h B ) ) ) ^ 2 ) -
                ( ( normh ` ( A -h ( _i .h B ) ) ) ^ 2 ) ) ) ) / 4 ) $=
      ( csp co cva cmv cmin ci cmul caddc c4 cdiv cno cfv cexp normsqi oveq12i
      c2 csm polid2i hvaddcli hvsubcli ax-icn hvmulcli oveq2i oveq1i eqtr4i ) A
      BEFABGFZUJEFZABHFZULEFZIFZJAJBUAFZGFZUPEFZAUOHFZUREFZIFZKFZLFZMNFUJOPTQFZ
      ULOPTQFZIFZJUPOPTQFZUROPTQFZIFZKFZLFZMNFABBACDDCUBVJVBMNVEUNVIVALVCUKVDUM
      IUJABCDUCRULABCDUDRSVHUTJKVFUQVGUSIUPAUOCJBUEDUFZUCRURAUOCVKUDRSUGSUHUI
      $.
      $( [30-Jun-2005] $)
  $}

  $( Polarization identity.  Recovers inner product from norm.  Exercise 4(a)
     of [ReedSimon] p. 63.  The outermost operation is + instead of - due to
     our mathematicians' (rather than physicists') version of axiom
     ~ ax-his3 . $)
  polid $p |- ( ( A e. ~H /\ B e. ~H ) ->
           ( A .ih B ) = ( ( ( ( ( normh ` ( A +h B ) ) ^ 2 ) -
              ( ( normh ` ( A -h B ) ) ^ 2 ) ) +
       ( _i x. ( ( ( normh ` ( A +h ( _i .h B ) ) ) ^ 2 ) -
                ( ( normh ` ( A -h ( _i .h B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
    ( csp co cva cno cfv c2 cexp cmv cmin ci cmul caddc cdiv c0v fveq2d oveq12d
    c4 oveq1d chil wcel csm wceq cif oveq1 oveq2d eqeq12d oveq2 ax-hv0cl elimel
    polidi dedth2h ) AUAUBZBUAUBZABCDZABEDZFGZHIDZABJDZFGZHIDZKDZLALBUCDZEDZFGZ
    HIDZAVDJDZFGZHIDZKDZMDZNDZSODZUDUNAPUEZBCDZVOBEDZFGZHIDZVOBJDZFGZHIDZKDZLVO
    VDEDZFGZHIDZVOVDJDZFGZHIDZKDZMDZNDZSODZUDVOUOBPUEZCDZVOWNEDZFGZHIDZVOWNJDZF
    GZHIDZKDZLVOLWNUCDZEDZFGZHIDZVOXCJDZFGZHIDZKDZMDZNDZSODZUDABPPAVOUDZUPVPVNW
    MAVOBCUFXNVMWLSOXNVCWCVLWKNXNUSVSVBWBKXNURVRHIXNUQVQFAVOBEUFQTXNVAWAHIXNUTV
    TFAVOBJUFQTRXNVKWJLMXNVGWFVJWIKXNVFWEHIXNVEWDFAVOVDEUFQTXNVIWHHIXNVHWGFAVOV
    DJUFQTRUGRTUHBWNUDZVPWOWMXMBWNVOCUIXOWLXLSOXOWCXBWKXKNXOVSWRWBXAKXOVRWQHIXO
    VQWPFBWNVOEUIQTXOWAWTHIXOVTWSFBWNVOJUIQTRXOWJXJLMXOWFXFWIXIKXOWEXEHIXOWDXDF
    XOVDXCVOEBWNLUCUIZUGQTXOWHXHHIXOWGXGFXOVDXCVOJXPUGQTRUGRTUHVOWNAPUAUJUKBPUA
    UJUKULUM $.
    $( [17-Nov-2007] $)

