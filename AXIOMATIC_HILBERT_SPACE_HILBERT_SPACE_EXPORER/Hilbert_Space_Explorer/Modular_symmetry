$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Modular symmetry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d q r C $.  $d c p q r A $.  $d c p q r B $.
    mdsymlem1.1 $e |- A e. CH $.
    mdsymlem1.2 $e |- B e. CH $.
    mdsymlem1.3 $e |- C = ( A vH p ) $.
    $( Lemma for ~ mdsymi . $)
    mdsymlem1 $p |- ( ( ( p e. CH /\ ( B i^i C ) C_ A ) /\
            ( B MH* A /\ p C_ ( A vH B ) ) ) -> p C_ A ) $=
      ( cv cch wcel cin wss wa chj co mpan2 syl6sseqr wceq mpan adantr cdmd wbr
      chub2 chjcomi sseq2i biimpi anim12i sylib ad2ant2rl chjcl syl5eqel anim2i
      ssin chub1 ancoms dmdi mp3anl1 mpanl1 syl2anc adantlr incom oveq1i chincl
      wb chlejb1 3syl biimpa syl5eq eqtr3d adantrr sseqtrd ) DHZIJZBCKZALZMZBAU
      AUBZVLABNOZLZMMVLCBANOZKZAVMVSVLWALZVOVQVMVSMVLCLZVLVTLZMWBVMWCVSWDVMVLAV
      LNOZCVMAIJZVLWELEVLAUCPGQVSWDVRVTVLABEFUDUEUFUGVLCVTUMUHUIVPVQWAARVSVPVQM
      CBKZANOZWAAVMVQWHWARZVOVMVQMCIJZVQACLZMZWIVMWJVQVMCWEIGWFVMWEIJEAVLUJSUKZ
      TVQVMWLVMWKVQVMAWECWFVMAWELEAVLUNSGQULUOWFWJWLWIEBIJZWFWJWLWIFBACUPUQURUS
      UTVPWHARVQVPWHVNANOZAWGVNANCBVAVBVMVOWOARZVMWJVNIJZVOWPVDZWMWNWJWQFBCVCSW
      QWFWREVNAVEPVFVGVHTVIVJVK $.
      $( [29-Jul-2007] $) $( [1-Jul-2004] $)

    $( Lemma for ~ mdsymi . $)
    mdsymlem2 $p |- ( ( ( p e. HAtoms /\ ( B i^i C ) C_ A ) /\
         ( B MH* A /\ p C_ ( A vH B ) ) ) -> ( B =/= 0H -> E. r e. HAtoms
         E. q e. HAtoms ( p C_ ( q vH r ) /\ ( q C_ A /\ r C_ B ) ) ) ) $=
      ( cv wss cat wrex wcel wa chj co cch atelch adantlr c0h wne cdmd hatomici
      cin wbr simplll chub1 syl2an mdsymlem1 sylanl1 adantr anim1i anass anasss
      jca sylib wceq oveq1 sseq2d sseq1 anbi12d rcla4ev syl2anc exp32 reximdvai
      anbi1d syl5 ) BUAUBDJZBKZDLMFJZLNZBCUEAKZOZBAUCUFVKABPQKOZOZVKEJZVIPQZKZV
      QAKZVJOZOZELMZDLMDBHUDVPVJWCDLVPVILNZVJWCVPWDVJOZOVLVKVKVIPQZKZVKAKZVJOZO
      ZWCVLVMVOWEUGVPWDVJWJVPWDOZVJOWGWHOZVJOWJWKWLVJWKWGWHVNWDWGVOVLWDWGVMVLVK
      RNZVIRNWGWDVKSZVISVKVIUHUITTVPWHWDVLWMVMVOWHWNABCFGHIUJUKULUPUMWGWHVJUNUQ
      UOWBWJEVKLVQVKURZVSWGWAWIWOVRWFVKVQVKVIPUSUTWOVTWHVJVQVKAVAVGVBVCVDVEVFVH
      $.
      $( [29-Jul-2007] $) $( [1-Jul-2004] $)

    $( Lemma for ~ mdsymi . $)
    mdsymlem3 $p |- ( ( ( ( p e. HAtoms /\ -. ( B i^i C ) C_ A ) /\
               p C_ ( A vH B ) ) /\ A =/= 0H ) ->
   E. r e. HAtoms E. q e. HAtoms ( p C_ ( q vH r ) /\ ( q C_ A /\ r C_ B ) ) )
        $=
      ( cv cat wcel wss wa chj co wrex cch wi adantlr cin wn c0h wb atelch mpan
      wne chjcl syl5eqel chincl chrelat2 mpan2 3syl biimpa ad2antrr ssin sseq2i
      sylancr biimpi adantl sylbir exp4b com34 com23 imp4b sylan2 adantrr com12
      atcvat4i imp wceq biimpcd necon3bd adantrl atnemeq0 ancoms syl5ib adantll
      sseq1 adantr chjcom syl2an sseq2d atexch syl3an1 3com13 3expa sylbid syld
      exp3a exp31 com24 imp3a anasss simprl a1i simpl ad2antrl jctird reximdvai
      jcad mpd exp32 ) FJZKLZBCUAZAMUBZNZXDABOPMZNZAUCUGZNZDJZXFMZXMAMZUBZNZDKQ
      ZXDEJZXMOPMZXSAMZXMBMZNZNZEKQZDKQXHXRXIXKXEXGXRXEXDRLZXFRLZXGXRUDZXDUEZYF
      BRLCRLYGHYFCAXDOPZRIARLZYFYJRLGAXDUHUFUIBCUJURYGYKYHGDXFAUKULUMUNUOXLXQYE
      DKXLXMKLZXQYEXLYLXQNZNZYAXMXDXSOPZMZNZEKQZYEXLYMYRXHXKYMYRSZXIXEXKYSXGYMX
      EXKNZYRYLXNYTYRSZXPXNYLXMYJMZUUAXNYBXMCMZNZUUBXMBCUPZUUCUUBYBUUCUUBCYJXMI
      UQUSUTVAYLUUBXEXKYRYLXEUUBXKYRSYLXEXKUUBYRYLXEXKUUBYREAXMXDGVIVBVCVDVEVFV
      GVHTTVJYNYQYDEKXJYMXSKLZYQYDSSZXKXHYMUUGXIXEYMUUGXGXEYMNZUUFYQYDUUHUUFYQN
      ZXTYCXEYLXQUUIXTSXEYLNZXQUUFYQXTUUJYQUUFXQXTUUJYAYPUUFXQXTSZSUUJUUFYPYAUU
      KUUJUUFYPYAUUKSUUJUUFNZYPNZYAXQXTUUMYAXQNZXSXMUAUCVKZXTUULUUNUUOSZYPYLUUF
      UUPXEUUNXSXMUGZYLUUFNUUOYAXPUUQXNYAXPUUQYAXOXSXMXSXMVKYAXOXSXMAVSVLVMVJVN
      UUFYLUUQUUOUDXSXMVOVPVQVRVTUULYPUUOXTSZUULYPXMXSXDOPZMZUURUULYOUUSXMXEUUF
      YOUUSVKZYLXEYFXSRLZUVAUUFYIXSUEZXDXSWAWBTWCUULUUTUUOXTXEYLUUFUUTUUONXTSZU
      UFYLXEUVDUUFUVBYLXEUVDUVCXSXMXDWDWEWFWGWJWHVJWIWJWKWLWMWLVEWNUUHUUIYAYBUU
      IYASUUHUUFYAYPWOWPYMYBXEXNYBYLXPXNUUDYBUUEYBUUCWQVAWRUTWSXAWJTTTWTXBXCWTX
      B $.
      $( [18-May-2007] $) $( [2-Jul-2004] $)

    $( Lemma for ~ mdsymi .  This is the forward direction of Lemma 4(i) of
       [Maeda] p. 168. $)
    mdsymlem4 $p |- ( p e. HAtoms -> ( ( B MH* A /\
            ( ( A =/= 0H /\ B =/= 0H ) /\ p C_ ( A vH B ) ) ) -> E. q e. HAtoms
               E. r e. HAtoms ( p C_ ( q vH r ) /\ ( q C_ A /\ r C_ B ) ) ) )
        $=
      ( cv cat c0h wne wa chj co wss wrex wi exp31 wcel wbr cin mdsymlem2 com4t
      cdmd ex com23 imp44 com3l wn mdsymlem3 anasss com3r ancoms adantlr adantl
      a1d pm2.61d rexcom syl6ib ) FJZKUAZBAUFUBZALMZBLMZNVBABOPQZNZNZVBEJZDJZOP
      QVJAQVKBQNNZEKRDKRZVLDKREKRVCBCUCAQZVIVMSVIVCVNVMVDVEVFVGVCVNVMSSZVDVFVGV
      OSSVEVDVGVFVOVDVGVFVOSVCVNVDVGNZVFVMVCVNVPVFVMSABCDEFGHIUDTUEUGUHURUIUJVI
      VCVNUKZVMVHVCVQVMSSZVDVEVGVRVFVGVEVRVCVQVGVENZVMVCVQVSVMVCVQNVGVEVMABCDEF
      GHIULUMTUNUOUPUQUJUSVLDEKKUTVA $.
      $( [30-Jul-2007] $) $( [2-Jul-2004] $)

    $( Lemma for ~ mdsymi . $)
    mdsymlem5 $p |- ( ( q e. HAtoms /\ r e. HAtoms ) -> ( -. q = p ->
                    ( ( p C_ ( q vH r ) /\ ( q C_ A /\ r C_ B ) ) ->
                    ( ( ( c e. CH /\ A C_ c ) /\ p e. HAtoms ) ->
                    ( p C_ c -> p C_ ( ( c i^i B ) vH A ) ) ) ) ) ) $=
      ( cv wcel wa cch wss chj co wi adantrl adantrr cat wceq wn cin w3a c0h wb
      df-ne atnemeq0 syl5bbr anbi2d 3adant3 atelch atexch syl3an1 sylbid 3com23
      wne exp3a 3expa adantrd imp32 simplrl anim1i ancoms chub2 mpan sstr chub1
      sylan2 mpan2 anim12i anandirs adantll chjcl syl3an3 adantr chlejb2 biimpa
      chlub mpbid sseqtrd exp45 anasss syl2an adantlr syl7 imp44 sstrd ad2antlr
      ad2ant2lr simplrr adantl ssin sylib chincl chlej1 ex syl3an2 3comr chlej2
      jca mp3anl2 sylanl2 adantllr sstr2 syl5com chjcom ad2antrr sseq1d sylibrd
      syld ad2antrl exp32 sylan imp31 mpd exp4d com34 imp4c com24 ) EKZUALZDKZU
      ALZMZGKZNLZAYGOZMFKZUALZMYJYBYDPQZOZYBAOZYDBOZMZMZYBYJUBUCZYJYGOZYJYGBUDZ
      APQZOZRZYFYHYIYKYQYRUUCRRZYFYHYKYIUUDYFYHYKYIUUDRYFYHYKMZMZYIYQYRUUCUUFYI
      YQYRMZMZYSUUBUUFUUHYSMZMZYDYTOZUUBUUJYDYGOZYOMUUKUUJUULYOUUJYDYBYJPQZYGUU
      FUUHYDUUMOZYSUUFUUGUUNYIUUFYQYRUUNUUFYMYRUUNRZYPYFYKYMUUORZYHYCYEYKUUPYCY
      KYEUUPYCYKYEUEZYMYRUUNUUQYMYRMZYMYBYJUDUFUBZMZUUNYCYKUURUUTUGYEYCYKMZYRUU
      SYMYRYBYJURUVAUUSYBYJUHYBYJUIUJUKULYCYBNLZYKYEUUTUUNRYBUMZYBYJYDUNUOUPUSU
      QUTSVAVBSTUUFYIUUGYSUUMYGOZUUGYNUUFYIYSUVDRZYMYNYOYRVCYCUUEYIYNUVERRZYEYC
      UVBYJNLZYHMZUVFUUEUVCYKYHUVHYKUVGYHYJUMVDVEUVBUVGYHUVFUVBUVGMZYHMZYIYNYSU
      VDUVJYIYNYSMZMMUUMYGAPQZYGUVJUVKUUMUVLOZYIUVJUVKMYBUVLOZYJUVLOZMZUVMYHUVK
      UVPUVIUVKYHUVPYNYSYHUVPYNYHMUVNYSYHMUVOYHYNAUVLOZUVNANLZYHUVQHAYGVFVGYBAU
      VLVHVJYHYSYGUVLOZUVOYHUVRUVSHYGAVIVKYJYGUVLVHVJVLVMVEVNUVJUVPUVMUGZUVKUVB
      UVGYHUVTYHUVBUVGUVLNLZUVTYHUVRUWAHYGAVOVKYBYJUVLVTVPUTVQWASYHYIUVLYGUBZUV
      IUVKYHYIUWBUVRYHYIUWBUGHAYGVRVGVSWKWBWCWDWEWFWGWHWIUUIYOUUFUUGYOYIYSYMYNY
      OYRWLWJWMXBYDYGBWNWOUUFUUHUUKUUBRZYSUUFUUGUWCYIUUFYQUWCYRUUFYMYPUWCUUFYMM
      YNUWCYOUUFYMYNUWCYFYHYMYNUWCRRZYKYFUVBYDNLZMZYHUWDYCUVBYEUWEUVCYDUMVLUWFY
      HMZYMYNUWCUWGYMYNMMUUKYLUUAOZUUBUWGYNUUKUWHRYMUWGYNMZUUKYDYBPQZYTYBPQZOZU
      WHUWGUUKUWLRZYNUVBUWEYHUWMUWEYHUVBUWMYHUWEYTNLZUVBUWMYHBNLUWNIYGBWPVKZUWE
      UWNUVBUEUUKUWLYDYTYBWQWRWSWTUTVQUWIUWLUWJUUAOZUWHUWIUWKUUAOZUWLUWPUVBYHYN
      UWQUWEYHUVBUWNYNUWQUWOUVBUVRUWNYNUWQHYBAYTXAXCXDXEUWJUWKUUAXFXGUWIYLUWJUU
      AUWFYLUWJUBYHYNYBYDXHXIXJXKXLSYMUWHUUBRUWGYNYJYLUUAXFXMXLXNXOTXPTWDTSTXQX
      NXRXNXSXTYA $.
      $( [2-Jul-2004] $)

    $( Lemma for ~ mdsymi .  This is the converse direction of Lemma 4(i) of
       [Maeda] p. 168, and is based on the proof of Theorem 1(d) to (e) of
       [Maeda] p. 167. $)
    mdsymlem6 $p |- ( A. p e. HAtoms ( p C_ ( A vH B ) ->
      E. q e. HAtoms E. r e. HAtoms ( p C_ ( q vH r ) /\ ( q C_ A /\ r C_ B ) )
        )
                   -> B MH* A ) $=
      ( vc cv chj co wss wa cat wi wral cch wcel wrex cin chjcomi sseq2i anbi2i
      cdmd wbr ssin bitri wceq mdsymlem5 sseq1 chincl mpan2 chub2 sylancr sstr2
      syl5 syl6bi imp3a a1i com13 adantrr ad2ant2r adantll com12 exp3a pm2.61d2
      rexlimivv imim2d com34 imp4b syl5bir ex ralimdva wb chjcli chjcl chrelat3
      sylancl syl2anc adantr sylibrd com3r ralrimiv dmdbr2 mp2an sylibr ) FKZAB
      LMZNZWIEKZDKZLMNZWLANZWMBNZOZOZDPUAEPUAZQZFPRZAJKZNZXBBALMZUBZXBBUBZALMZN
      ZQZJSRZBAUFUGZXAXIJSXBSTZXCXAXHXLXCXAXHQXLXCOZXAWIXENZWIXGNZQZFPRZXHXMWTX
      PFPXMWIPTZOZWTXPXNWIXBNZWKOZXSWTOXOYAXTWIXDNZOXNWKYBXTWJXDWIABGHUCUDUEWIX
      BXDUHUIXSWTXTWKXOXSWTWKXTXOXSWSXTXOQZWKWSXSYCWRXSYCQZEDPPWLPTWMPTOWLWIUJZ
      WRYDQABCDEFJGHIUKYEWRXSYCWRXSOYEYCWQXSYEYCQZWNWOXMYFWPXRWOXLYFXCXTYEWOXLO
      ZXOYEYGXOQQXTYEWOXLXOYEWOWIANZXLXOQWLWIAULXLAXGNZYHXOXLASTZXFSTZYIGXLBSTZ
      YKHXBBUMUNZAXFUOUPWIAXGUQURUSUTVAVBVCVDVEVFVGVHVIVFVJVKVLVMVNVOXLXHXQVPZX
      CXLXESTZXGSTZYNXLXDSTYOBAHGVQXBXDUMUNXLYKYJYPYMGXFAVRVTFXEXGVSWAWBWCVNWDW
      EYLYJXKXJVPHGJBAWFWGWH $.
      $( [30-Jul-2007] $) $( [2-Jul-2004] $)

    $( Lemma for ~ mdsymi .  Lemma 4(i) of [Maeda] p. 168.  Note that Maeda's
       1965 definition of dual modular pair has reversed arguments compared to
       the later (1970) definition given in Remark 29.6 of [MaedaMaeda] p. 130,
       which is the one that we use. $)
    mdsymlem7 $p |- ( ( A =/= 0H /\ B =/= 0H ) ->
                    ( B MH* A <->
   A. p e. HAtoms ( p C_ ( A vH B ) -> E. q e. HAtoms E. r e. HAtoms
            ( p C_ ( q vH r ) /\ ( q C_ A /\ r C_ B ) ) ) ) ) $=
      ( c0h wne wa cdmd wbr cv chj co wss cat wrex wi wral wcel mdsymlem4 exp4d
      com13 ralrimdv mdsymlem6 impbid1 ) AJKBJKLZBAMNZFOZABPQRZULEOZDOZPQRUNARU
      OBRLLDSTESTZUAZFSUBUJUKUQFSULSUCZUKUJUQURUKUJUMUPABCDEFGHIUDUEUFUGABCDEFG
      HIUHUI $.
      $( [30-Jul-2007] $) $( [3-Jul-2004] $)

    $( Lemma for ~ mdsymi .  Lemma 4(ii) of [Maeda] p. 168. $)
    mdsymlem8 $p |- ( ( A =/= 0H /\ B =/= 0H ) -> ( B MH* A <-> A MH* B ) ) $=
      ( vq vr c0h wne wa cv chj co wss cat wrex wb wcel wi wral cdmd wbr sseq2i
      chjcomi cch atelch chjcom syl2an sseq2d ancom a1i anbi12d 2rexbiia rexcom
      wceq bitri imbi12i ralbii mdsymlem7 eqid ancoms 3bitr4d ) AJKZBJKZLZDMZAB
      NOZPZVHHMZIMZNOZPZVKAPZVLBPZLZLZIQRHQRZUAZDQUBZVHBANOZPZVHVLVKNOZPZVPVOLZ
      LZHQRIQRZUAZDQUBZBAUCUDABUCUDZWAWJSVGVTWIDQVJWCVSWHVIWBVHABEFUFUEVSWGIQRH
      QRWHVRWGHIQQVKQTZVLQTZLZVNWEVQWFWNVMWDVHWLVKUGTVLUGTVMWDUQWMVKUHVLUHVKVLU
      IUJUKVQWFSWNVOVPULUMUNUOWGHIQQUPURUSUTUMABCIHDEFGVAVFVEWKWJSBABVHNOZHIDFE
      WOVBVAVCVD $.
      $( [30-Jul-2007] $) $( [3-Jul-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    mdsym.1 $e |- A e. CH $.
    mdsym.2 $e |- B e. CH $.
    $( M-symmetry of the Hilbert lattice.  Lemma 5 of [Maeda] p. 168. $)
    mdsymi $p |- ( A MH B <-> B MH A ) $=
      ( vx cort cfv c0h wceq cmd wbr wb wss wa chil fveq2 pjococi choc0 mp3an12
      chssii 3eqtr3g syl5sseqr cch wcel ssmd1 ssmd2 jca pm5.1 wo wn wne neanior
      3syl cdmd cv chj choccli eqid mdsymlem8 mddmd mp2an 3bitr4g sylbir ecase3
      co ) BFGZHIZAFGZHIZABJKZBAJKZLZVGABMZVJVKNZVLVGOABACTVGVFFGHFGZBOVFHFPBDQ
      RUAUBVMVJVKAUCUDZBUCUDZVMVJCDABUESVPVQVMVKCDABUFSUGVJVKUHZUMVIBAMZVNVLVIO
      BABDTVIVHFGVOAOVHHFPACQRUAUBVSVJVKVQVPVSVJDCBAUFSVQVPVSVKDCBAUESUGVRUMVGV
      IUIUJVFHUKVHHUKNZVLVFHVHHULVTVHVFUNKZVFVHUNKZVJVKVFVHVFEUOUPVEZEBDUQACUQW
      CURUSVPVQVJWALCDABUTVAVQVPVKWBLDCBAUTVAVBVCVD $.
      $( [3-Jul-2004] $)
  $}

  $( M-symmetry of the Hilbert lattice.  Lemma 5 of [Maeda] p. 168. $)
  mdsym $p |- ( ( A e. CH /\ B e. CH ) -> ( A MH B <-> B MH A ) ) $=
    ( cch wcel cmd wbr wb chil cif wceq breq1 breq2 bibi12d helch elimel mdsymi
    dedth2h ) ACDZBCDZABEFZBAEFZGRAHIZBEFZBUBEFZGUBSBHIZEFZUEUBEFZGABHHAUBJTUCU
    AUDAUBBEKAUBBELMBUEJUCUFUDUGBUEUBELBUEUBEKMUBUEAHCNOBHCNOPQ $.
    $( [14-Jun-2006] $) $( [6-Jul-2004] $)

  $( Dual M-symmetry of the Hilbert lattice. $)
  dmdsym $p |- ( ( A e. CH /\ B e. CH ) -> ( A MH* B <-> B MH* A ) ) $=
    ( cch wcel wa cort cfv cmd cdmd wb choccl mdsym syl2an dmdmd ancoms 3bitr4d
    wbr ) ACDZBCDZEAFGZBFGZHQZUATHQZABIQBAIQZRTCDUACDUBUCJSAKBKTUALMABNSRUDUCJB
    ANOP $.
    $( [25-Jul-2007] $)

  $( Two Hilbert lattice elements have the dual modular pair property if the
     second is an atom. $)
  atdmd2 $p |- ( ( A e. CH /\ B e. HAtoms ) -> A MH* B ) $=
    ( cch wcel cat wa cdmd wbr atdmd ancoms wb atelch dmdsym sylan2 mpbird ) AC
    DZBEDZFABGHZBAGHZQPSBAIJQPBCDRSKBLABMNO $.
    $( [12-Jul-2008] $) $( [6-Jul-2004] $)

  ${
    $d x y z w A $.  $d x y z w B $.  $d y z w C $.
    sumdmdi.1 $e |- A e. CH $.
    sumdmdi.2 $e |- B e. CH $.
    $( If the subspace sum of two Hilbert lattice elements is closed, then the
       elements are a dual modular pair.  Remark in [MaedaMaeda] p. 139. $)
    sumdmdii $p |- ( ( A +H B ) = ( A vH B ) -> A MH* B ) $=
      ( vx vy vz vw co wceq cv wss cin wi cch wcel wa wrex csh wb cph wral cdmd
      chj wbr ineq2 adantr wel elin cva chseli cmv ssel2 chsh shsubcl 3exp syl7
      syl exp4a com23 imp41 adantlr chil chel cheli w3a hvsubadd ax-hvcom eqcom
      eqeq1d syl6bb 3adant1 bitrd 3com23 syl3an 3expa eleq1 syl6bir mpbid simpr
      imp jca exp31 reximdvai r19.42v syl6ib reximdva ancom bitri anass rexbii2
      anbi1i syl6ibr chshii shincl sylancl ad2antrr shsel sylibrd expimpd ssrdv
      syl5bi adantl eqsstr3d chincl mpan2 chslej ad2antrl sstrd ralrimiv dmdbr2
      exp32 mp2an sylibr ) ABUAIZABUDIZJZBEKZLZXRXPMZXRAMZBUDIZLZNZEOUBZABUCUEZ
      XQYDEOXQXROPZXSYCXQYGXSQZQZXTYABUAIZYBYIXTXRXOMZYJXQYKXTJYHXOXPXRUFUGYHYK
      YJLXQYHFYKYJFKZYKPFEUHZYLXOPZQYHYLYJPZYLXRXOUIYHYMYNYOYNYLGKZHKZUJIZJZHBR
      ZGARZYHYMQZYOGHABYLCDUKUUBUUAYTGYARZYOUUBUUAGEUHZYTQZGARUUCUUBYTUUEGAUUBY
      PAPZQZYTUUDYSQZHBRUUEUUGYSUUHHBUUGYQBPZYSUUHUUGUUIQZYSQZUUDYSUUKYLYQULIZX
      RPZUUDUUJUUMYSUUBUUIUUMUUFYGXSYMUUIUUMYGYMXSUUIUUMNYGYMXSUUIUUMXSUUIQHEUH
      ZYGYMUUMBXRYQUMYGXRSPZYMUUNUUMNNXRUNZUUOYMUUNUUMYLYQXRUOUPURUQUSUTVAVBUGU
      UJYSUUMUUDTZUUJYSUULYPJZUUQUUBUUFUUIUURYSTZUUBYLVCPZUUFYPVCPZUUIYQVCPZUUS
      YGYMUUTXSYLXRVDVBYPACVEYQBDVEUUTUVBUVAUUSUUTUVBUVAVFUURYQYPUJIZYLJZYSYLYQ
      YPVGUVBUVAUVDYSTUUTUVBUVAQZUVDYRYLJYSUVEUVCYRYLYQYPVHVJYRYLVIVKVLVMVNVOVP
      UULYPXRVQVRWAVSUUJYSVTWBWCWDUUDYSHBWEWFWGYTUUEGYAAYPYAPZYTQUUFUUDQZYTQUUF
      UUEQUVFUVGYTUVFUUDUUFQUVGYPXRAUIUUDUUFWHWIWLUUFUUDYTWJWIWKWMUUBYASPZBSPYO
      UUCTYGUVHXSYMYGUUOASPUVHUUPACWNXRAWOWPWQBDWNGHYABYLWRWPWSXBWTXBXAXCXDYGYJ
      YBLZXQXSYGYAOPZBOPZUVIYGAOPZUVJCXRAXEXFDYABXGWPXHXIXLXJUVLUVKYFYETCDEABXK
      XMXN $.
      $( [25-Jul-2007] $) $( [12-Jul-2004] $)

    $( Commuting subspaces form a modular pair. $)
    cmmdi $p |- ( A C_H B -> A MH B ) $=
      ( ccm wbr cort cfv cdmd cmd cph co chj cmcm4i choccli osumcor2i sylbi cch
      wceq wcel sumdmdii syl wb mddmd mp2an sylibr ) ABEFZAGHZBGHZIFZABJFZUGUHU
      IKLUHUIMLSZUJUGUHUIEFULABCDNUHUIACOZBDOZPQUHUIUMUNUAUBARTBRTUKUJUCCDABUDU
      EUF $.
      $( [16-Jan-2005] $)

    $( Commuting subspaces form a dual modular pair. $)
    cmdmdi $p |- ( A C_H B -> A MH* B ) $=
      ( cort cfv ccm wbr cmd cdmd choccli cmmdi cmcm4i wcel dmdmd mp2an 3imtr4i
      cch wb ) AEFZBEFZGHTUAIHZABGHABJHZTUAACKBDKLABCDMARNBRNUCUBSCDABOPQ $.
      $( [25-Apr-2006] $)

    $( Lemma for ~ sumdmdi .  The span of vector ` C ` not in the subspace sum
       is "trimmed off." $)
    sumdmdlem $p |- ( ( C e. ~H /\ -. C e. ( A +H B ) ) ->
                    ( ( B +H ( span ` { C } ) ) i^i A ) = ( B i^i A ) ) $=
      ( vz vw chil wcel co wa cv wi cva wceq csh wb c0v adantr exp32 vy cph csn
      cspn cfv cin elin wrex chshii spansnsh shsel sylancr cmv cheli elspansncl
      w3a hvsubadd eqcom syl6bb syl3an 3expa shsvsi eleq1 syl5ibcom com4r imp31
      wn sylbird adantrr shscli elspansn5 ax-mp mpdd oveq2 ax-hvaddid sylan9eqr
      adantl sylan eqeq2d adantll biimpac biimparc biimpri ancoms expr ad2antrl
      sylan2 mpd ex com23 com4l imp4c exp4a exp3a rexlimdvv sylbid imp4b syl5bi
      a1d ssrdv wss shsub1 mpan ssrin 3syl eqssd ) CHIZCABUBJZIVGZKZBCUCUDUEZUB
      JZAUFZBAUFZXJUAXMXNUALZXMIXOXLIZXOAIZKXJXOXNIZXOXLAUGXGXIXPXQXRXGXPXIXQXR
      MZXGXPXOFLZGLZNJZOZGXKUHFBUHZXIXSMZXGBPIZXKPIZXPYDQBEUIZCUJZFGBXKXOUKULXG
      YCYEFGBXKXGXTBIZYAXKIZKZYCYEXQXGYLYCKZXIXRXQYMXGXIXRMXQYMXGXIXRXQYJYKYCXJ
      XRMZYCXQYJYKYNYCXQYJYKYNMYCXQYJKZKZXJYKXRYPXJYKXRMYPXJKZYKYAROZXRYQYKYAXH
      IZYRYPXGYKYSMZXIYCYOXGYTYOXGYKYCYSYOXGYKYCYSMYOXGYKKZKYCXOXTUMJZYAOZYSXQY
      JUUAUUCYCQZXQXOHIZYJXTHIZUUAYAHIZUUDXOADUNXTBEUNZCYAUOUUEUUFUUGUPUUCYBXOO
      YCXOXTYAUQYBXOURUSUTVAYOUUCYSMUUAYOUUBXHIUUCYSABXOXTADUIZYHVBUUBYAXHVCVDS
      VHTVEVFVIXJYKYSYRMMYPXJYKYSYRXHPIXJYKYSKKYRMABUUIYHVJXHCYAVKVLTVQVMYPYKYR
      XRMZMXJYPUUJYKYCYOYRXRYCYOYRKZKXOXTOZXRUUKYCUULYJYRYCUULQXQYJYRKYBXTXOYJU
      UFYRYBXTOUUHYRUUFYBXTRNJXTYARXTNVNXTVOVPVRVSVTWAYOUULXRMYCYRXQYJUULXRYJUU
      LKXQXOBIZXRUULUUMYJXOXTBVCWBUUMXQXRXRUUMXQKXOBAUGWCWDWGWEWFWHWEWSSVMWIWJT
      WKWLWMWJWKWNWOWPWJWQWRWTXGXNXMXAZXIXGYGBXLXAZUUNYIYFYGUUOYHBXKXBXCBXLAXDX
      ESXF $.
      $( [18-Dec-2004] $)

    $( Lemma for ~ sumdmdi . $)
    sumdmdlem2 $p |- ( A. x e. HAtoms ( ( x vH B ) i^i ( A vH B ) ) C_
                  ( ( ( x vH B ) i^i A ) vH B ) -> ( A +H B ) = ( A vH B ) ) $=
      ( chj co cin wss cat cph wa wceq wcel wi cspn c0v ineq1d oveq1d syl6eq vy
      cv wral chil chjcli cheli wn csn cfv csh spansnsh chshii sylancl spansnid
      shsub2 sseldd ad2antrl elin wne df-ne spansna sylan2br sseq12d rcla4v syl
      oveq1 wb spansnj spansnch chjcom sylan2 eqtrd mpan adantr sylibrd expdimp
      cch com12 ssid c0h fveq2d spansn0 oveq2d shs0i inss1 chub2i ssini chincli
      sneq eqssi chjcomi chabs1i eqtri mpbiri pm2.61d2 adantrr sumdmdlem sseq1d
      shsub2i adantl sstrd sseld syl5bir mpand exp32 com34 pm2.18 pm2.43d ssrdv
      syl8 syl5 chsleji jctil eqss sylibr ) AUBZCFGZBCFGZHZXQBHZCFGZIZAJUCZBCKG
      ZXRIZXRYDIZLYDXRMYCYFYEYCUAXRYDYCUAUBZXRNZYGYDNZYHYGUDNZYCYHYIOZYGXRBCDEU
      EUFYCYJYHYIUGZYIOYIYCYJYLYHYIYCYJYLYKYCYJYLLZLZYGCYGUHZPUIZKGZNZYHYIYJYRY
      CYLYJYPYQYGYJYPUJNCUJNYPYQIYGUKCEULZYPCUOUMYGUNUPUQYRYHLYGYQXRHZNYNYIYGYQ
      XRURYNYTYDYGYNYTYQBHZCFGZYDYCYJYTUUBIZYLYCYJLYGQMZUUCYCYJUUDUGZUUCYJUUELZ
      YCUUCUUFYCYPCFGZXRHZUUGBHZCFGZIZUUCUUFYPJNZYCUUKOUUEYJYGQUSUULYGQUTYGVAVB
      YBUUKAYPJXPYPMZXSUUHYAUUJUUMXQUUGXRXPYPCFVFZRUUMXTUUICFUUMXQUUGBUUNRSVCVD
      VEYJUUCUUKVGUUEYJYTUUHUUBUUJYJYQUUGXRCVQNZYJYQUUGMEUUOYJLYQCYPFGZUUGCYGVH
      YJUUOYPVQNUUPUUGMYGVICYPVJVKVLVMZRYJUUAUUICFYJYQUUGBUUQRSVCVNVOVRVPUUDUUC
      CCICVSZUUDYTCUUBCUUDYTCXRHZCUUDYQCXRUUDYQCVTKGCUUDYPVTCKUUDYPQUHZPUIVTUUD
      YOUUTPYGQWIWAWBTWCCYSWDTZRUUSCCXRWECCXRUURCBEDWFWGWJTUUDUUBCBHZCFGZCUUDUU
      AUVBCFUUDYQCBUVARSUVCCUVBFGCUVBCCBEDWHEWKCBEDWLWMZTVCWNWOWPYMUUBYDIZYCYMU
      VECYDICBYSBDULWSYMUUBCYDYMUUBUVCCYMUUAUVBCFBCYGDEWQSUVDTWRWNWTXAXBXCXDXEX
      FYIXGXJXKXHXIBCDEXLXMYDXRXNXO $.
      $( [23-Dec-2004] $)

    $( The subspace sum of two Hilbert lattice elements is closed iff the
       elements are a dual modular pair.  Theorem 2 of [Holland] p. 1519. $)
    sumdmdi $p |- ( ( A +H B ) = ( A vH B ) <-> A MH* B ) $=
      ( vx cph co chj wceq cdmd wbr sumdmdii cv cin wss cat wral cch wcel wb
      dmdbr4 mp2an atelch imim1i ralimi2 sylbi sumdmdlem2 syl impbii ) ABFGABHG
      ZIZABJKZABCDLULEMZBHGZUJNUNANBHGOZEPQZUKULUOERQZUPARSBRSULUQTCDEABUAUBUOU
      OERPUMPSUMRSUOUMUCUDUEUFEABCDUGUHUI $.
      $( [5-Aug-2007] $) $( [14-Dec-2004] $)

    $( Dual modular pair property in terms of atoms. $)
    dmdbr4ati $p |- ( A MH* B <-> A. x e. HAtoms
            ( ( x vH B ) i^i ( A vH B ) ) C_ ( ( ( x vH B ) i^i A ) vH B ) ) $=
      ( cdmd wbr cv chj co cin wss cat wral cch wcel wb dmdbr4 mp2an atelch cph
      imim1i ralimi2 sylbi wceq sumdmdlem2 sumdmdi sylib impbii ) BCFGZAHZCIJZB
      CIJZKULBKCIJLZAMNZUJUNAONZUOBOPCOPUJUPQDEABCRSUNUNAOMUKMPUKOPUNUKTUBUCUDU
      OBCUAJUMUEUJABCDEUFBCDEUGUHUI $.
      $( [13-Jul-2008] $) $( [15-Jan-2005] $)

    $( Dual modular pair property in terms of atoms. $)
    dmdbr5ati $p |- ( A MH* B <->
   A. x e. HAtoms ( x C_ ( A vH B ) -> x C_ ( ( ( x vH B ) i^i A ) vH B ) ) )
        $=
      ( chj co wss cin wi cat wral wcel cch wceq wa adantr mpan2 3syl wb wbr cv
      cdmd atelch dmdi4 mp3an12 syl5 a1dd ralrimiv cph wn chjcom sylancr ineq1d
      com12 chjcomi ineq2i syl6eqr sseq2i notbii atabs2i sylan2b eqtr3d sylancl
      imp chjcl chincl chub2 mpan eqsstrd ex biantrud pm4.83 ralbiia sumdmdlem2
      syl6bb sylbi sumdmdi sylib impbii chub2i biantru chjcli chlub syl5bb ssid
      mp3an23 biantrur ssin bitri biimpa inss1 jctil sylibr sseq1d mp3an2 mpdan
      eqss bitrd bitr4d pm5.74da syl ) BCUCUAZAUBZBCFGZHZXDCFGZXEIZXGBIZCFGZHZJ
      ZAKLZXFXDXJHZJZAKLXCXMXCXLAKXCXDKMZXKXFXPXDNMZXCXKXDUDZXQXCXKBNMZCNMZXQXC
      XKJDEBCXDUEUFUOUGUHUIXMBCUJGXEOZXCXMXKAKLYAXLXKAKXPXLXLXFUKZXKJZPXKXPYCXL
      XPYBXKXPYBPZXHCXJYDCXDFGZCBFGZIZXHCXPYGXHOYBXPYGXGYFIXHXPYEXGYFXPXTXQYEXG
      OEXRCXDULUMUNXEYFXGBCDEUPZUQURQYBXPXDYFHZUKZYGCOZXFYIXEYFXDYHUSUTXPYJYKCB
      XDEDVAVEVBVCXPCXJHZYBXPXGNMZXINMZYLXPXQXTYMXREXDCVFZVDYMXSYNDXGBVGRZXTYNY
      LECXIVHVIZSQVJVKVLXFXKVMVPVNABCDEVOVQBCDEVRVSVTXLXOAKXPXQXLXOTXRXQXFXKXNX
      QXFPZXKXGXJHZXNYRXHXGXJYRXHXGHZXGXHHZPXHXGOYRUUAYTXQXFUUAXQXFXGXEHZUUAXFX
      FCXEHZPZXQUUBUUCXFCBEDWAWBXQXTXENMUUDUUBTEBCDEWCXDCXEWDWGWEUUBXGXGHZUUBPU
      UAUUEUUBXGWFWHXGXGXEWIWJVPWKXGXEWLWMXHXGWRWNWOXQXNYSTXFXQXNXNYLPZYSXQYLXN
      XQYMYNYLXQXTYMEYORZYPYQSVLXQXJNMZUUFYSTZXQYMYNUUHUUGYPYNXTUUHEXICVFRSXQXT
      UUHUUIEXDCXJWDWPWQWSQWTXAXBVNWJ $.
      $( [12-Aug-2007] $) $( [14-Jan-2005] $)

    $( Dual modular pair property in terms of atoms.  The modular law takes the
       form of the shearing identity. $)
    dmdbr6ati $p |- ( A MH* B <-> A. x e. HAtoms
            ( ( A vH B ) i^i x ) = ( ( ( ( x vH B ) i^i A ) vH B ) i^i x ) ) $=
      ( cdmd wbr chj co cv cin wceq cat wral cch wcel wb dmdbr3 incom wss mp2an
      wa inass 3eqtri chabs2 mpan2 ineq2d syl5reqr adantr ineq1 adantl ralimiaa
      eqtr4d sylbi atelch imim1i ralimi2 syl wi inss1 sseq1 mpbiri df-ss biimpi
      syl5eq sseq1d syl5ibcom ralimi dmdbr5ati sylibr impbii ) BCFGZBCHIZAJZKZV
      NCHIZBKCHIZVNKZLZAMNZVLVSAONZVTVLVQVPVMKZLZAONZWABOPCOPZVLWDQDEABCRUAWCVS
      AOVNOPZWCUBVOWBVNKZVRWFVOWGLWCWFWGVMVNVPKZKZVOWIWHVMKVNWBKWGVMWHSVNVPVMUC
      VNWBSUDWFWHVNVMWFWEWHVNLEVNCUEUFUGUHUIWCVRWGLWFVQWBVNUJUKUMULUNVSVSAOMVNM
      PWFVSVNUOUPUQURVTVNVMTZVNVQTZUSZAMNVLVSWLAMVSVOVQTZWJWKVSWMVRVQTVQVNUTVOV
      RVQVAVBWJVOVNVQWJVOVNVMKZVNVMVNSWJWNVNLVNVMVCVDVEVFVGVHABCDEVIVJVK $.
      $( [13-Feb-2008] $) $( [18-Jan-2005] $)

    $( Dual modular pair property in terms of atoms. $)
    dmdbr7ati $p |- ( A MH* B <->
       A. x e. HAtoms ( ( A vH B ) i^i x ) C_ ( ( ( x vH B ) i^i A ) vH B ) )
        $=
      ( cdmd wbr chj co cv cin wss cat wral dmdbr6ati inss1 sseq1 mpbiri ralimi
      wceq sylbi wi sseqin2 biimpi sseq1d biimpcd dmdbr5ati sylibr impbii ) BCF
      GZBCHIZAJZKZULCHIBKCHIZLZAMNZUJUMUNULKZTZAMNUPABCDEOURUOAMURUOUQUNLUNULPU
      MUQUNQRSUAUPULUKLZULUNLZUBZAMNUJUOVAAMUSUOUTUSUMULUNUSUMULTULUKUCUDUEUFSA
      BCDEUGUHUI $.
      $( [14-Jul-2008] $) $( [18-Jan-2005] $)
  $}

  ${
    mdoc1.1 $e |- A e. CH $.
    $( Orthocomplements form a modular pair. $)
    mdoc1i $p |- A MH ( _|_ ` A ) $=
      ( cort cfv ccm wbr cmd cmidi cmcm2ii choccli cmmdi ax-mp ) AACDZEFAMGFAAB
      BABHIAMBABJKL $.
      $( [29-Apr-2006] $)

    $( Orthocomplements form a modular pair. $)
    mdoc2i $p |- ( _|_ ` A ) MH A $=
      ( cort cfv cmd choccli mdoc1i ococi breqtri ) ACDZJCDAEJABFGABHI $.
      $( [29-Apr-2006] $)

    $( Orthocomplements form a dual modular pair. $)
    dmdoc1i $p |- A MH* ( _|_ ` A ) $=
      ( cort cfv ccm wbr cdmd cmidi cmcm2ii choccli cmdmdi ax-mp ) AACDZEFAMGFA
      ABBABHIAMBABJKL $.
      $( [29-Apr-2006] $)

    $( Orthocomplements form a dual modular pair. $)
    dmdoc2i $p |- ( _|_ ` A ) MH* A $=
      ( cort cfv cdmd choccli dmdoc1i ococi breqtri ) ACDZJCDAEJABFGABHI $.
      $( [29-Apr-2006] $)
  $}

  ${
    mdcompl.1 $e |- A e. CH $.
    mdcompl.2 $e |- B e. CH $.
    $( A condition equivalent to the modular pair property.  Part of proof of
       Theorem 1.14 of [MaedaMaeda] p. 4. $)
    mdcompli $p |- ( A MH B <-> ( A i^i ( _|_ ` ( A i^i B ) ) ) MH
                  ( B i^i ( _|_ ` ( A i^i B ) ) ) ) $=
      ( cin cort cfv cmd wbr cdmd wss chj co chincli mdoc1i dmdoc2i ssid chjcli
      wb chil chssii chjoi sseqtr4i choccli mdslmd1i mp4an ) ABEZUGFGZHIUHUGJIU
      GUGKABLMZUGUHLMZKABHIAUHEBUHEHISUGABCDNZOUGUKPUGQUITUJUIABCDRUAUGUKUBUCUG
      UHABUKUGUKUDCDUEUF $.
      $( [29-Apr-2006] $)

    $( A condition equivalent to the dual modular pair property. $)
    dmdcompli $p |- ( A MH* B <-> ( A i^i ( _|_ ` ( A i^i B ) ) ) MH*
                  ( B i^i ( _|_ ` ( A i^i B ) ) ) ) $=
      ( cin cort cfv cmd wbr cdmd wss chj co chincli mdoc1i dmdoc2i ssid chjcli
      wb chil chssii chjoi sseqtr4i choccli mdsldmd1i mp4an ) ABEZUGFGZHIUHUGJI
      UGUGKABLMZUGUHLMZKABJIAUHEBUHEJISUGABCDNZOUGUKPUGQUITUJUIABCDRUAUGUKUBUCU
      GUHABUKUGUKUDCDUEUF $.
      $( [29-Apr-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    mddmdin0.1 $e |- A e. CH $.
    mddmdin0.2 $e |- B e. CH $.
    mddmdin0.3 $e |- A. x e. CH A. y e. CH ( ( x MH* y /\ ( x i^i y ) = 0H ) ->
                     x MH y ) $.
    $( If dual modular implies modular whenever meet is zero, then dual modular
       implies modular for arbitrary lattice elements.  This theorem is needed
       for the remark after Lemma 7 of [Holland] p. 1524 to hold. $)
    mddmdin0i $p |- ( A MH* B -> A MH B ) $=
      ( cin cdmd wbr cmd c0h wceq chincli cv wa wi cch wral wcel inindir eqtr3i
      cort cfv chocini choccli breq1 ineq1 eqeq1d anbi12d imbi12d breq2 rcla42v
      ineq2 mp2an ax-mp mpan2 dmdcompli mdcompli 3imtr4i ) CCDHZUCUDZHZDVBHZIJZ
      VCVDKJZCDIJCDKJVEVCVDHZLMZVFVAVBHVGLCDVBUAVACDEFNZUEUBAOZBOZIJZVJVKHZLMZP
      ZVJVKKJZQZBRSARSZVEVHPZVFQZGVCRTVDRTVRVTQCVBEVAVIUFZNDVBFWANVQVTVCVKIJZVC
      VKHZLMZPZVCVKKJZQABVCVDRRVJVCMZVOWEVPWFWGVLWBVNWDVJVCVKIUGWGVMWCLVJVCVKUH
      UIUJVJVCVKKUGUKVKVDMZWEVSWFVFWHWBVEWDVHVKVDVCIULWHWCVGLVKVDVCUNUIUJVKVDVC
      KULUKUMUOUPUQCDEFURCDEFUSUT $.
      $( [29-Apr-2006] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.
    cdjreu.1 $e |- A e. SH $.
    cdjreu.2 $e |- B e. SH $.
    $( A member of the sum of disjoint subspaces has a unique decomposition.
       Part of Lemma 5 of [Holland] p. 1520. $)
    cdjreui $p |- ( ( C e. ( A +H B ) /\ ( A i^i B ) = 0H ) ->
                 E! x e. A E. y e. B C = ( x +h y ) ) $=
      ( vz vw co wcel c0h wceq wa cv cva wrex wi chil sheli cph cin wral shseli
      weq wreu biimpi reeanv eqtr2 cmv wb anim12i hvaddsub4 syl2an an4s adantll
      shsubcl mp3an1 ancoms eleq1 syl5ibrcom adantl adantr jctild eleq2 syl5bbr
      csh elin ad2antrr sylibd elch0 hvsubeq0 syl5bb ad2antlr sylbid rexlimdvva
      c0v syl5 syl5bir ralrimivva oveq1 eqeq2d rexbidv oveq2 syl6bb reu4 sylibr
      cbvrexv ) ECDUAJKZCDUBZLMZNEAOZBOZPJZMZBDQZACQZWPEHOZIOZPJZMZIDQZNZAHUEZR
      ZHCUCACUCZNWPACUFWIWQWKXFWIWQABCDEFGUDUGWKXEAHCCXCWOXANZIDQBDQWKWLCKZWRCK
      ZNZNZXDWOXABIDDUHXKXGXDBIDDXGWNWTMZXKWMDKZWSDKZNZNZXDEWNWTUIXPXLWLWRUJJZW
      SWMUJJZMZXDXJXOXLXSUKZWKXHXMXIXNXTXHXMNWLSKZWMSKZNWRSKZWSSKZNXTXIXNNXHYAX
      MYBWLCFTZWMDGTULXIYCXNYDWRCFTZWSDGTULWLWMWRWSUMUNUOUPXPXSXQLKZXDXPXSXQCKZ
      XQDKZNZYGXJXOXSYJRWKXJXONXSYIYHXOXSYIRXJXOYIXSXRDKZXNXMYKDVGKXNXMYKGWSWMD
      UQURUSXQXRDUTVAVBXJYHXOCVGKXHXIYHFWLWRCUQURVCVDUPWKYJYGUKXJXOYJXQWJKWKYGX
      QCDVHWJLXQVEVFVIVJXJYGXDUKZWKXOXHYAYCYLXIYEYFYGXQVQMYAYCNXDXQVKWLWRVLVMUN
      VNVJVOVRVPVSVTULWPXBAHCXDWPEWRWMPJZMZBDQXBXDWOYNBDXDWNYMEWLWRWMPWAWBWCYNX
      ABIDBIUEYMWTEWMWSWRPWDWBWHWEWFWG $.
      $( [20-May-2005] $)
  $}

  ${
    $d x y z w A $.  $d x y z w v B $.
    cdj1.1 $e |- A e. SH $.
    cdj1.2 $e |- B e. SH $.
    $( Two ways to express " ` A ` and ` B ` are completely disjoint
       subspaces."  (1) => (2) in Lemma 5 of [Holland] p. 1520. $)
    cdj1i $p |- ( E. w e. RR ( 0 < w /\ A. y e. A A. v e. B
      ( ( normh ` y ) + ( normh ` v ) ) <_ ( w x. ( normh ` ( y +h v ) ) ) ) ->
                    E. x e. RR ( 0 < x /\ A. y e. A A. z e. B
                    ( ( normh ` y ) = 1 -> x <_ ( normh ` ( y -h z ) ) ) ) ) $=
      ( cc0 wbr cno cfv co cle wa c1 wceq cr wcel cv clt caddc cva cmul wral wi
      cmv wrex cdiv wne gt0ne0 rereccl syldan adantrr recgt0 csm 1re a1i ax-1cn
      cneg cc negcli sheli hvmulcl sylancr normcl adantl readdcl adantr hvsubcl
      chil syl2an remulcl sylan2 anassrs normge0 wb addge01 mpan biimpa syl2anc
      syl ad2antlr csh shmulcl mp3an12 fveq2 oveq2d oveq2 fveq2d breq12d rcla4v
      ad2ant2lr oveq1 eqcoms ad2antll hvsubval adantll 3brtr4d letrd ex simplll
      imp adantllr simpllr lediv1 mp3an1 syl12anc recnd recn ad3antrrr ad2antrr
      sylibd divcan3 syl3anc breqtrd exp43 com23 ralrimdv ralimdva jca32 imbi2d
      impr breq2 breq1 2ralbidv anbi12d rcla4ev syl6 rexlimiv ) JDUAZUBKZBUAZLM
      ZEUAZLMZUCNZYLYNYPUDNZLMZUENZOKZEGUFZBFUFZPZJAUAZUBKZYOQRZUUFYNCUAZUHNZLM
      ZOKZUGZCGUFBFUFZPZASUIZDSYLSTZUUEQYLUJNZSTZJUURUBKZUUHUURUUKOKZUGZCGUFZBF
      UFZPZPZUUPUUQUUEUVFUUQUUEPUUSUUTUVDUUQYMUUSUUDUUQYMYLJUKZUUSYLULZYLUMUNUO
      UUQYMUUTUUDYLUPUOUUQYMUUDUVDUUQYMPZUUCUVCBFUVIYNFTZPZUUCUVBCGUVKUUIGTZUUC
      UVBUVKUVLUUCUUHUVAUVKUVLPZUUCUUHPZPUURYLUUKUENZYLUJNZUUKOUVMUVNUURUVPOKZU
      VMUVNQUVOOKZUVQUUQUVJUVLUVNUVRUGYMUUQUVJPZUVLPZUVNUVRUVTUVNPZQQQVAZUUIUQN
      ZLMZUCNZUVOQSTZUWAURUSUVTUWESTZUVNUVTUWFUWDSTZUWGURUVLUWHUVSUVLUWCVLTZUWH
      UVLUWBVBTZUUIVLTZUWIQUTVCZUUIGIVDZUWBUUIVEVFZUWCVGWCZVHQUWDVIVFVJUVTUVOST
      ZUVNUUQUVJUVLUWPUVJUVLPZUUQUUKSTZUWPUWQUUJVLTZUWRUVJYNVLTZUWKUWSUVLYNFHVD
      ZUWMYNUUIVKVMZUUJVGZWCZYLUUKVNZVOVPVJUVLQUWEOKZUVSUVNUVLUWHJUWDOKZUXFUWOU
      VLUWIUXGUWNUWCVQWCUWHUXGUXFUWFUWHUXGUXFVRURQUWDVSVTWAWBWDUWAYOUWDUCNZYLYN
      UWCUDNZLMZUENZUWEUVOOUVLUUCUXHUXKOKZUVSUUHUVLUUCUXLUVLUWCGTZUUCUXLUGGWETU
      WJUVLUXMIUWLUWBUUIGWFWGUUBUXLEUWCGYPUWCRZYRUXHUUAUXKOUXNYQUWDYOUCYPUWCLWH
      WIUXNYTUXJYLUEUXNYSUXILYPUWCYNUDWJWKWIWLWMWCXDWNUUHUWEUXHRZUVTUUCUXOQYOQY
      OUWDUCWOWPWQUVTUVOUXKRZUVNUVJUVLUXPUUQUWQUUKUXJYLUEUWQUUJUXILUVJUWTUWKUUJ
      UXIRUVLUXAUWMYNUUIWRVMWKWIWSVJWTXAXBXEUVMUWPUUQYMUVRUVQVRZUVMUUQUWRUWPUUQ
      YMUVJUVLXCZUVMUWSUWRUVJUVLUWSUVIUXBWSUXCWCUXEWBUXRUUQYMUVJUVLXFUWFUWPUVIU
      XQURQUVOYLXGXHXIXNXDUVMUVPUUKRZUVNUVMUUKVBTZYLVBTZUVGUXSUVJUVLUXTUVIUWQUU
      KUXDXJWSUUQUYAYMUVJUVLYLXKXLUVIUVGUVJUVLUVHXMUUKYLXOXPVJXQXRXSXTYAYDYBXBU
      UOUVEAUURSUUFUURRZUUGUUTUUNUVDUUFUURJUBYEUYBUUMUVBBCFGUYBUULUVAUUHUUFUURU
      UKOYFYCYGYHYIYJYK $.
      $( [6-Oct-2006] $) $( [21-May-2005] $)

    $( A property of " ` A ` and ` B ` are completely disjoint subspaces."
       Part of Lemma 5 of [Holland] p. 1520. $)
    cdj3lem1 $p |- ( E. x e. RR ( 0 < x /\ A. y e. A A. z e. B
      ( ( normh ` y ) + ( normh ` z ) ) <_ ( x x. ( normh ` ( y +h z ) ) ) ) ->
                    ( A i^i B ) = 0H ) $=
      ( cc0 wbr cno cfv caddc co cva cmul cle wa wceq wcel c2 vw cv clt cin c0h
      wral cr wss c0v c1 cneg csm elin csh ax-1cn negcli shmulcl mp3an12 anim2i
      wi cc sylbi fveq2 oveq1d oveq1 fveq2d oveq2d breq12d oveq2 rcla42v adantl
      syl chil wb shincli sheli normneg normcl recnd 2times eqtr4d norm0 syl6eq
      hvnegid recn mul01 sylan9eqr 2cn mul01i syl6eqr 0re pm3.2i lemul2 mp3an23
      2re 2pos normge0 bitr3d letri3 sylancl norm-i 3bitr2d bitrd sylan2 sylibd
      biantrud impancom elch0 syl6ibr ssrdv shle0 ax-mp syl6ib adantld rexlimiv
      ex ) HAUBZUCIZBUBZJKZCUBZJKZLMZXQXSYANMZJKZOMZPIZCEUFBDUFZQDEUDZUERZAUGXQ
      UGSZYHYJXRYKYHYIUEUHZYJYKYHYLYKYHQZUAYIUEYMUAUBZYISZYNUIRZYNUESYKYOYHYPYK
      YOQYHYNJKZUJUKZYNULMZJKZLMZXQYNYSNMZJKZOMZPIZYPYOYHUUEUTZYKYOYNDSZYSESZQZ
      UUFYOUUGYNESZQUUIYNDEUMUUJUUHUUGEUNSYRVASUUJUUHGUJUOUPYRYNEUQURUSVBYGUUEY
      QYBLMZXQYNYANMZJKZOMZPIBCYNYSDEXSYNRZYCUUKYFUUNPUUOXTYQYBLXSYNJVCVDUUOYEU
      UMXQOUUOYDUULJXSYNYANVEVFVGVHYAYSRZUUKUUAUUNUUDPUUPYBYTYQLYAYSJVCVGUUPUUM
      UUCXQOUUPUULUUBJYAYSYNNVIVFVGVHVJVLVKYOYKYNVMSZUUEYPVNYNYIDEFGVOZVPYKUUQQ
      ZUUETYQOMZTHOMZPIZYPUUSUUAUUTUUDUVAPUUQUUAUUTRYKUUQUUAYQYQLMZUUTUUQYTYQYQ
      LYNVQVGUUQYQVASUUTUVCRUUQYQYNVRZVSYQVTVLWAVKUUSUUDHUVAUUQYKUUDXQHOMZHUUQU
      UCHXQOUUQUUCUIJKHUUQUUBUIJYNWDVFWBWCVGYKXQVASUVEHRXQWEXQWFVLWGTWHWIWJVHUU
      QUVBYPVNYKUUQUVBYQHPIZHYQPIZQZYQHRZYPUUQUVFUVBUVHUUQYQUGSZUVFUVBVNZUVDUVJ
      HUGSZTUGSZHTUCIZQUVKWKUVMUVNWOWPWLYQHTWMWNVLUUQUVGUVFYNWQXFWRUUQUVJUVLUVI
      UVHVNUVDWKYQHWSWTYNXAXBVKXCXDXEXGYNXHXIXJXPYIUNSYLYJVNUURYIXKXLXMXNXO $.
      $( [23-May-2005] $)
  $}

  ${
    $d x y z w v u t h A $.  $d x y z w v u t h B $.  $d x y z w v u C $.
    $d x y z w D $.
    cdj3lem2.1 $e |- A e. SH $.
    cdj3lem2.2 $e |- B e. SH $.
    ${
      $d v u t h S $.
      cdj3lem2.3 $e |- S = { <. x , y >. | ( x e. ( A +H B ) /\
                     y = U. { z e. A | E. w e. B x = ( z +h w ) } ) } $.
      $( Lemma for ~ cdj3i .  Value of the first-component function ` S ` . $)
      cdj3lem2 $p |- ( ( C e. A /\ D e. B /\ ( A i^i B ) = 0H ) ->
                    ( S ` ( C +h D ) ) = C ) $=
        ( wcel wceq cva co cv wrex crab cuni cin c0h w3a cfv cph shsvai rexbidv
        wa eqeq1 rabbidv unieqd csh elexi rabex uniex fvopab4 syl 3adant3 oveq2
        eqid eqeq2d rcla4ev mpan2 3ad2ant2 wreu simp1 cdjreui sylan 3impa oveq1
        wb reuuni2 syl2anc mpbid eqtrd ) GEMZHFMZEFUAUBNZUCZGHOPZIUDZVTCQZDQZOP
        ZNZDFRZCESZTZGVPVQWAWHNZVRVPVQUHZVTEFUEPZMZWIEFGHJKUFZABVTAQZWDNZDFRZCE
        SZTWHWKIWNVTNZWQWGWRWPWFCEWRWOWEDFWNVTWDUIUGUJUKLWGWFCEEULJUMUNUOUPUQUR
        VSVTGWCOPZNZDFRZWHGNZVQVPXAVRVQVTVTNZXAVTUTWTXCDHFWCHNWSVTVTWCHGOUSVAVB
        VCVDVSVPWFCEVEZXAXBVKVPVQVRVFVPVQVRXDWJWLVRXDWMCDEFVTJKVGVHVIWFXACEGWBG
        NZWEWTDFXEWDWSVTWBGWCOVJVAUGVLVMVNVO $.
        $( [23-May-2005] $)

      $( Lemma for ~ cdj3i .  Closure of the first-component function
         ` S ` . $)
      cdj3lem2a $p |- ( ( C e. ( A +H B ) /\ ( A i^i B ) = 0H ) ->
                    ( S ` C ) e. A ) $=
        ( vv vu wceq co wcel cfv cv wrex wa cin c0h cph cva shseli w3a cdj3lem2
        simp1 eqeltrd 3expa fveq2 eleq1d syl5ibr exp3a rexlimdvv syl5bi impcom
        com13 ) EFUAUBNZGEFUCOPZGHQZEPZUTGLRZMRZUDOZNZMFSLESUSVBLMEFGIJUEUSVFVB
        LMEFVFVCEPZVDFPZTZUSVBVFVIUSVBVIUSTVBVFVEHQZEPZVGVHUSVKVGVHUSUFVJVCEABC
        DEFVCVDHIJKUGVGVHUSUHUIUJVFVAVJEGVEHUKULUMUNURUOUPUQ $.
        $( [25-May-2005] $)

      $( Lemma for ~ cdj3i .  The first-component function ` S ` is bounded if
         the subspaces are completely disjoint. $)
      cdj3lem2b $p |- ( E. v e. RR ( 0 < v /\ A. x e. A A. y e. B
      ( ( normh ` x ) + ( normh ` y ) ) <_ ( v x. ( normh ` ( x +h y ) ) ) ) ->
                    E. v e. RR ( 0 < v /\ A. u e. ( A +H B )
                      ( normh ` ( S ` u ) ) <_ ( v x. ( normh ` u ) ) ) ) $=
        ( wbr cno cfv co cle wa cr wcel vt vh cin c0h wceq cc0 cv clt caddc cva
        cmul wral wrex cph cdj3lem1 wi shseli biimpi fveq2 oveq1d fveq2d oveq2d
        oveq1 breq12d oveq2 rcla42v cdj3lem2 3expa ad2ant2r chil normge0 adantl
        sheli syl wb normcl addge01 syl2an mpbid adantr readdcl hvaddcl remulcl
        ad2antrr sylan2 letr syl3anc mpand imp an32s adantrl eqbrtrd syl5ibrcom
        ancoms exp31 syld com14 com4t rexlimdvv syl5com com3l ralrimdv reximdva
        anim2d mpcom ) GHUCUDUEZUFEUGZUHMZAUGZNOZBUGZNOZUIPZXGXIXKUJPZNOZUKPZQM
        ZBHULAGULZRZESUMXHFUGZIOZNOZXGXTNOZUKPZQMZFGHUNPZULZRZESUMEABGHJKUOXFXS
        YHESXFXGSTZRZXRYGXHYJXRYEFYFXTYFTZYJXRYEYKXTUAUGZUBUGZUJPZUEZUBHUMUAGUM
        ZYJXRYEUPZYKYPUAUBGHXTJKUQURYJYOYQUAUBGHYOXRYJYLGTZYMHTZRZYEYTXRYJYOYEY
        TXRYLNOZYMNOZUIPZXGYNNOZUKPZQMZYJYOYEUPZUPXQUUFUUAXLUIPZXGYLXKUJPZNOZUK
        PZQMABYLYMGHXIYLUEZXMUUHXPUUKQUULXJUUAXLUIXIYLNUSUTUULXOUUJXGUKUULXNUUI
        NXIYLXKUJVCVAVBVDXKYMUEZUUHUUCUUKUUEQUUMXLUUBUUAUIXKYMNUSVBUUMUUJUUDXGU
        KUUMUUIYNNXKYMYLUJVEVAVBVDVFYTUUFYJUUGYTUUFRZYJRZYEYOYNIOZNOZUUEQMUUOUU
        QUUAUUEQYTXFUUQUUAUEUUFYIYTXFRUUPYLNYRYSXFUUPYLUEABCDGHYLYMIJKLVGVHVAVI
        UUNYIUUAUUEQMZXFYTYIUUFUURYTYIRZUUFUURUUSUUAUUCQMZUUFUURYTUUTYIYTUFUUBQ
        MZUUTYSUVAYRYSYMVJTZUVAYMHKVMZYMVKVNVLYRUUASTZUUBSTZUVAUUTVOYSYRYLVJTZU
        VDYLGJVMZYLVPVNZYSUVBUVEUVCYMVPVNZUUAUUBVQVRVSVTUUSUVDUUCSTZUUESTZUUTUU
        FRUURUPYRUVDYSYIUVHWDYTUVJYIYRUVDUVEUVJYSUVHUVIUUAUUBWAVRVTYIYTUVKYTYIU
        UDSTZUVKYTYNVJTZUVLYRUVFUVBUVMYSUVGUVCYLYMWBVRYNVPVNXGUUDWCWEWNUUAUUCUU
        EWFWGWHWIWJWKWLYOYBUUQYDUUEQYOYAUUPNXTYNIUSVAYOYCUUDXGUKXTYNNUSVBVDWMWO
        WPWQWRWSWTXAXBXDXCXE $.
        $( [26-May-2005] $)
    $}

    ${
      $d v u t h T $.
      cdj3lem3.3 $e |- T = { <. x , y >. | ( x e. ( A +H B ) /\
                     y = U. { w e. B | E. z e. A x = ( z +h w ) } ) } $.
      $( Lemma for ~ cdj3i .  Value of the second-component function ` T ` . $)
      cdj3lem3 $p |- ( ( C e. A /\ D e. B /\ ( A i^i B ) = 0H ) ->
                    ( T ` ( C +h D ) ) = D ) $=
        ( wcel wceq cva co wa chil sheli cv cin c0h cfv incom eqeq1i w3a syl2an
        ax-hvcom fveq2d 3adant3 cph wrex crab cuni copab eleq2i eqeq2d rexbidva
        shscomi rabbiia unieqi eqeq2i anbi12i opabbii eqtr4i cdj3lem2 syl3an3b
        eqtr3d 3com12 ) HFMZGEMZEFUAZUBNZGHOPZIUCZHNZVMVJVKFEUAZUBNZVPVLVQUBEFU
        DUEVJVKVRUFHGOPZIUCZVOHVJVKVTVONVRVJVKQVSVNIVJHRMGRMVSVNNVKHFKSGEJSHGUH
        UGUIUJABDCFEHGIKJIATZEFUKPZMZBTZWACTZDTZOPZNZCEULZDFUMZUNZNZQZABUOWAFEU
        KPZMZWDWAWFWEOPZNZCEULZDFUMZUNZNZQZABUOLXBWMABWOWCXAWLWNWBWAFEKJUSUPWTW
        KWDWSWJWRWIDFWFFMZWQWHCEXCWEEMZQWPWGWAXCWFRMWERMWPWGNXDWFFKSWEEJSWFWEUH
        UGUQURUTVAVBVCVDVEVFVHVGVI $.
        $( [23-May-2005] $)

      $( Lemma for ~ cdj3i .  Closure of the second-component function
         ` T ` . $)
      cdj3lem3a $p |- ( ( C e. ( A +H B ) /\ ( A i^i B ) = 0H ) ->
                    ( T ` C ) e. B ) $=
        ( vv vu wceq co wcel cfv cv wrex wa cin c0h cph cva shseli w3a cdj3lem3
        simp2 eqeltrd 3expa fveq2 eleq1d syl5ibr exp3a rexlimdvv syl5bi impcom
        com13 ) EFUAUBNZGEFUCOPZGHQZFPZUTGLRZMRZUDOZNZMFSLESUSVBLMEFGIJUEUSVFVB
        LMEFVFVCEPZVDFPZTZUSVBVFVIUSVBVIUSTVBVFVEHQZFPZVGVHUSVKVGVHUSUFVJVDFABC
        DEFVCVDHIJKUGVGVHUSUHUIUJVFVAVJFGVEHUKULUMUNURUOUPUQ $.
        $( [26-May-2005] $)

      $( Lemma for ~ cdj3i .  The second-component function ` T ` is bounded if
         the subspaces are completely disjoint. $)
      cdj3lem3b $p |- ( E. v e. RR ( 0 < v /\ A. x e. A A. y e. B
      ( ( normh ` x ) + ( normh ` y ) ) <_ ( v x. ( normh ` ( x +h y ) ) ) ) ->
                    E. v e. RR ( 0 < v /\ A. u e. ( A +H B )
                      ( normh ` ( T ` u ) ) <_ ( v x. ( normh ` u ) ) ) ) $=
        ( cno cfv co cva cmul cle wral wcel vt vh cc0 cv clt wbr caddc wrex cph
        wa cr wceq crab cuni copab shscomi eleq2i chil ax-hvcom syl2an rexbidva
        sheli eqeq2d rabbiia unieqi eqeq2i anbi12i eqtr4i cdj3lem2b fveq2 oveq1
        opabbii oveq1d fveq2d oveq2d breq12d oveq2 cbvral2v ralcom normcl recnd
        syl addcom ralbidva ralbiia bitr2i 3bitri anbi2i rexbii raleqi 3imtr4i
        cc ) UCEUDZUEUFZAUDZMNZBUDZMNZUGOZWMWOWQPOZMNZQOZRUFZBGSZAHSZUJZEUKUHWN
        FUDZINMNWMXGMNQORUFZFHGUIOZSZUJZEUKUHWNXCBHSAGSZUJZEUKUHWNXHFGHUIOZSZUJ
        ZEUKUHABDCEFHGIKJIWOXNTZWQWOCUDZDUDZPOZULZCGUHZDHUMZUNZULZUJZABUOWOXITZ
        WQWOXSXRPOZULZCGUHZDHUMZUNZULZUJZABUOLYNYFABYGXQYMYEXIXNWOHGKJUPUQYLYDW
        QYKYCYJYBDHXSHTZYIYACGYOXRGTZUJYHXTWOYOXSURTXRURTYHXTULYPXSHKVBXRGJVBXS
        XRUSUTVCVAVDVEVFVGVLVHVIXMXFEUKXLXEWNXLUAUDZMNZUBUDZMNZUGOZWMYQYSPOZMNZ
        QOZRUFZUBHSUAGSUUEUAGSUBHSZXEXCUUEYRWRUGOZWMYQWQPOZMNZQOZRUFABUAUBGHWOY
        QULZWSUUGXBUUJRUUKWPYRWRUGWOYQMVJVMUUKXAUUIWMQUUKWTUUHMWOYQWQPVKVNVOVPW
        QYSULZUUGUUAUUJUUDRUULWRYTYRUGWQYSMVJVOUULUUIUUCWMQUULUUHUUBMWQYSYQPVQV
        NVOVPVRUUEUAUBGHVSXEWRWPUGOZWMWQWOPOZMNZQOZRUFZBGSZAHSUUFXDUURAHWOHTZXC
        UUQBGUUSWQGTZUJZWSUUMXBUUPRUUSWPWLTWRWLTWSUUMULUUTUUSWPUUSWOURTZWPUKTWO
        HKVBZWOVTWBWAUUTWRUUTWQURTZWRUKTWQGJVBZWQVTWBWAWPWRWCUTUVAXAUUOWMQUVAWT
        UUNMUUSUVBUVDWTUUNULUUTUVCUVEWOWQUSUTVNVOVPWDWEUUQUUEWRYTUGOZWMWQYSPOZM
        NZQOZRUFABUBUAHGWOYSULZUUMUVFUUPUVIRUVJWPYTWRUGWOYSMVJVOUVJUUOUVHWMQUVJ
        UUNUVGMWOYSWQPVQVNVOVPWQYQULZUVFUUAUVIUUDRUVKWRYRYTUGWQYQMVJVMUVKUVHUUC
        WMQUVKUVGUUBMWQYQYSPVKVNVOVPVRWFWGWHWIXPXKEUKXOXJWNXHFXNXIGHJKUPWJWHWIW
        K $.
        $( [31-May-2005] $)
    $}
  $}

  ${
    $d x y z w v u t h f g A $.  $d x y z w v u t h f g B $.
    $d v u t h f g S $.  $d v u t h f g T $.
    cdj3.1 $e |- A e. SH $.
    cdj3.2 $e |- B e. SH $.
    cdj3.3 $e |- S = { <. x , y >. | ( x e. ( A +H B ) /\
                   y = U. { z e. A | E. w e. B x = ( z +h w ) } ) } $.
    cdj3.4 $e |- T = { <. x , y >. | ( x e. ( A +H B ) /\
                   y = U. { w e. B | E. z e. A x = ( z +h w ) } ) } $.
    cdj3.5 $e |- ( ph <-> E. v e. RR ( 0 < v /\ A. u e. ( A +H B )
                      ( normh ` ( S ` u ) ) <_ ( v x. ( normh ` u ) ) ) ) $.
    cdj3.6 $e |- ( ps <-> E. v e. RR ( 0 < v /\ A. u e. ( A +H B )
                      ( normh ` ( T ` u ) ) <_ ( v x. ( normh ` u ) ) ) ) $.
    $( Two ways to express " ` A ` and ` B ` are completely disjoint
       subspaces."  (1) <=> (3) in Lemma 5 of [Holland] p. 1520. $)
    cdj3i $p |- ( E. v e. RR ( 0 < v /\ A. x e. A A. y e. B
      ( ( normh ` x ) + ( normh ` y ) ) <_ ( v x. ( normh ` ( x +h y ) ) ) )
         <-> ( ( A i^i B ) = 0H /\ ph /\ ps ) ) $=
      ( cle cr vf vg vt vh cc0 cv clt wbr cno cfv caddc co cva cmul wral wa cin
      wrex c0h wceq cdj3lem1 cdj3lem2b sylibr cdj3lem3b 3jca breq2 oveq1 breq2d
      w3a cph ralbidv anbi12d cbvrexv bitri anbi12i reeanv bitr4i an4 wi addgt0
      wcel ex adantl shsvai fveq2 fveq2d oveq2d breq12d rcla4v anim12d syl chil
      sheli normcl anim12i hvaddcl syl2an remulcl sylan2 adantlr adantll le2add
      syl12anc wb cdj3lem2 breq1d cdj3lem3 3expa ancoms cc recnd adddir 3imtr4d
      recn syl3an syld ralrimdvva readdcl oveq1d oveq2 cbvral2v 2ralbidv syl5bb
      rcla4ev syl2and syl5bi rexlimdvva 3impib impbii ) UEGUFZUGUHZCUFZUIUJZDUF
      ZUIUJZUKULZYJYLYNUMULZUIUJZUNULZSUHZDJUOCIUOZUPZGTURZIJUQUSUTZABVIUUCUUDA
      BGCDIJMNVAUUCYKHUFZKUJZUIUJZYJUUEUIUJZUNULZSUHZHIJVJULZUOZUPZGTURZACDEFGH
      IJKMNOVBQVCUUCYKUUELUJZUIUJZUUISUHZHUUKUOZUPZGTURZBCDEFGHIJLMNPVDRVCVEUUD
      ABUUCABUPZUEUAUFZUGUHZUUGUVBUUHUNULZSUHZHUUKUOZUPZUEUBUFZUGUHZUUPUVHUUHUN
      ULZSUHZHUUKUOZUPZUPZUBTURUATURZUUDUUCUVAUVGUATURZUVMUBTURZUPUVOAUVPBUVQAU
      UNUVPQUUMUVGGUATYJUVBUTZYKUVCUULUVFYJUVBUEUGVFUVRUUJUVEHUUKUVRUUIUVDUUGSY
      JUVBUUHUNVGVHVKVLVMVNBUUTUVQRUUSUVMGUBTYJUVHUTZYKUVIUURUVLYJUVHUEUGVFUVSU
      UQUVKHUUKUVSUUIUVJUUPSYJUVHUUHUNVGVHVKVLVMVNVOUVGUVMUAUBTTVPVQUUDUVNUUCUA
      UBTTUVNUVCUVIUPZUVFUVLUPZUPUUDUVBTWAZUVHTWAZUPZUPZUUCUVCUVFUVIUVLVRUWEUVT
      UEUVBUVHUKULZUGUHZUWAUCUFZUIUJZUDUFZUIUJZUKULZUWFUWHUWJUMULZUIUJZUNULZSUH
      ZUDJUOUCIUOZUUCUWDUVTUWGVSUUDUWDUVTUWGUVBUVHVTWBWCUWEUWAUWPUCUDIJUWEUWHIW
      AZUWJJWAZUPZUPZUWAUWMKUJZUIUJZUVBUWNUNULZSUHZUWMLUJZUIUJZUVHUWNUNULZSUHZU
      PZUWPUWTUWAUXJVSZUWEUWTUWMUUKWAZUXKIJUWHUWJMNWDUXLUVFUXEUVLUXIUVEUXEHUWMU
      UKUUEUWMUTZUUGUXCUVDUXDSUXMUUFUXBUIUUEUWMKWEWFUXMUUHUWNUVBUNUUEUWMUIWEZWG
      WHWIUVKUXIHUWMUUKUXMUUPUXGUVJUXHSUXMUUOUXFUIUUEUWMLWEWFUXMUUHUWNUVHUNUXNW
      GWHWIWJWKWCUXAUWIUXDSUHZUWKUXHSUHZUPZUWLUXDUXHUKULZSUHZUXJUWPUWDUWTUXQUXS
      VSZUUDUWDUWTUPZUWITWAZUWKTWAZUPZUXDTWAZUXHTWAZUXTUWTUYDUWDUWRUYBUWSUYCUWR
      UWHWLWAZUYBUWHIMWMZUWHWNWKUWSUWJWLWAZUYCUWJJNWMZUWJWNWKWOWCUWBUWTUYEUWCUW
      TUWBUWNTWAZUYEUWTUWMWLWAZUYKUWRUYGUYIUYLUWSUYHUYJUWHUWJWPWQUWMWNWKZUVBUWN
      WRWSWTUWCUWTUYFUWBUWTUWCUYKUYFUYMUVHUWNWRWSXAUWIUWKUXDUXHXBXCXAUUDUWTUXJU
      XQXDZUWDUWTUUDUYNUWRUWSUUDUYNUWRUWSUUDVIZUXEUXOUXIUXPUYOUXCUWIUXDSUYOUXBU
      WHUICDEFIJUWHUWJKMNOXEWFXFUYOUXGUWKUXHSUYOUXFUWJUICDEFIJUWHUWJLMNPXGWFXFV
      LXHXIWTUWDUWTUWPUXSXDUUDUYAUWOUXRUWLSUWBUWCUWTUWOUXRUTZUWBUVBXJWAUWCUVHXJ
      WAUWTUWNXJWAUYPUVBXNUVHXNUWTUWNUYMXKUVBUVHUWNXLXOXHVHXAXMXPXQUWDUWGUWQUPZ
      UUCVSZUUDUWDUWFTWAZUYRUVBUVHXRUYSUYQUUCUUBUYQGUWFTYJUWFUTZYKUWGUUAUWQYJUW
      FUEUGVFUUAUWLYJUWNUNULZSUHZUDJUOUCIUOUYTUWQYTVUBUWIYOUKULZYJUWHYNUMULZUIU
      JZUNULZSUHCDUCUDIJYLUWHUTZYPVUCYSVUFSVUGYMUWIYOUKYLUWHUIWEXSVUGYRVUEYJUNV
      UGYQVUDUIYLUWHYNUMVGWFWGWHYNUWJUTZVUCUWLVUFVUASVUHYOUWKUWIUKYNUWJUIWEWGVU
      HVUEUWNYJUNVUHVUDUWMUIYNUWJUWHUMXTWFWGWHYAUYTVUBUWPUCUDIJUYTVUAUWOUWLSYJU
      WFUWNUNVGVHYBYCVLYDWBWKWCYEYFYGYFYHYI $.
      $( [1-Jun-2005] $)
  $}

$(

