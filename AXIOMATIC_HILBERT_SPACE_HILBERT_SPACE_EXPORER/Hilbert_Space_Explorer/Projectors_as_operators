$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Projectors as operators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z H $.
    pjhmop.1 $e |- H e. CH $.
    $( A projector is a Hermitian operator. $)
    pjhmopi $p |- ( proj ` H ) e. HrmOp $=
      ( vx vy cpj cfv cho wcel chil wf cv csp co wceq wral pjadji eqcomd rgen2a
      pjfi wa elhmop mpbir2an ) AEFZGHIIUCJCKZDKZUCFLMZUDUCFUELMZNZDIOCIOABSUHC
      DIUDIHUEIHTUGUFUDUEABPQRCDUCUAUB $.
      $( [24-Mar-2006] $)

    $( A projector is a linear operator. $)
    pjlnopi $p |- ( proj ` H ) e. LinOp $=
      ( cpj cfv cho wcel clo pjhmopi hmoplin ax-mp ) ACDZEFKGFABHKIJ $.
      $( [24-Mar-2006] $)

    $( The operator norm of a projector on a nonzero closed subspace is one.
       Part of Theorem 26.1 of [Halmos] p. 43. $)
    pjnmopi $p |- ( H =/= 0H -> ( normop ` ( proj ` H ) ) = 1 ) $=
      ( vy vx vz vw cfv cv cno c1 cle wbr wceq wa chil wrex cxr clt cr wcel c0h
      wne cpj cnop cab csup wf pjfi nmopval ax-mp wral vex eqeq1 anbi2d rexbidv
      wi elab cch pjnorm mpan pjhcli normcl syl letr mp3an3 syl2anc mpand breq1
      1re biimparc sylan expl rexlimiv sylbi rgen cheli adantr eqle pjid fveq2d
      imp simpr eqtr2d jca32 reximi2 chne0i chshii norm1exi bitri elexi 3imtr4i
      c0v breq2 rcla4ev ex ralrimivw wss nmopsetretHIL ressxr sstri rexr supxr2
      mpanl12 sylancr syl5eq ) AUAUBZAUCGZUDGZCHZIGZJKLZDHZXIXGGZIGZMZNZCOPZDUE
      ZQRUFZJOOXGUGZXHXSMABUHZDCXGUIUJXFEHZJKLZEXRUKZYBJRLZYBFHZRLZFXRPZUPZESUK
      ZXSJMZYCEXRYBXRTXKYBXNMZNZCOPZYCXQYNDYBEULXLYBMZXPYMCOYOXOYLXKXLYBXNUMUNU
      OUQYMYCCOXIOTZXKYLYCYPXKNXNJKLZYLYCYPXKYQYPXNXJKLZXKYQAURTZYPYRBXIAUSUTYP
      XNSTZXJSTZYRXKNYQUPZYPXMOTYTXIABVAXMVBVCXIVBZYTUUAJSTZUUBVIXNXJJVDVEVFVGW
      AYLYCYQYBXNJKVHVJVKVLVMVNVOXFYIESXFYEYHXFJXRTZYEYHXJJMZCAPZXKJXNMZNZCOPZX
      FUUEUUFUUICAOXIATZUUFNZYPXKUUHUUKYPUUFXIABVPZVQUUKUUAUUFXKUUKYPUUAUUMUUCV
      CXJJVRVKUULXNXJJUUKXNXJMUUFUUKXMXIIYSUUKXMXIMBXIAVSUTVTVQUUKUUFWBWCWDWEXF
      XIWLUBCAPUUGCABWFCCAABWGWHWIXQUUJDJJSVIWJXLJMZXPUUICOUUNXOUUHXKXLJXNUMUNU
      OUQWKYGYEFJXRYFJYBRWMWNVKWOWPXRQWQJQTZYDYJNYKXRSQXTXRSWQYADCXGWRUJWSWTUUD
      UUOVIJXAUJEFXRJXBXCXDXE $.
      $( [9-Apr-2006] $)

    $( A projector is a bounded linear operator. $)
    pjbdlni $p |- ( proj ` H ) e. BndLinOp $=
      ( cpj cfv cbo wcel clo cnop cr pjlnopi cch c0h fveq2 fveq2d eleq1d wne c1
      wceq cc0 ch0o pjnmopi 1re syl6eqel adantl df-h0op fveq2i nmop0 eqtr3i 0re
      eqeltri a1i pm2.61ne ax-mp elbdop2 mpbir2an ) ACDZEFUPGFUPHDZIFZABJAKFZUR
      BUSURLCDZHDZIFZALALRZUQVAIVCUPUTHALCMNOALPZURUSVDUQQIABUAUBUCUDVBUSVASITH
      DVASTUTHUEUFUGUHUIUJUKULUMUPUNUO $.
      $( [3-Jun-2006] $)
  $}

  $( A projection is a Hermitian operator. $)
  pjhmop $p |- ( H e. CH -> ( proj ` H ) e. HrmOp ) $=
    ( cch wcel cpj cfv cho c0h wceq fveq2 eleq1d h0elch elimel pjhmopi dedth
    cif ) ABCZADEZFCPAGOZDEZFCAGARHQSFARDIJRAGBKLMN $.
    $( [24-Apr-2006] $)

  ${
    $d f k x y z T $.
    hmopidmch.1 $e |- T e. HrmOp $.
    hmopidmch.2 $e |- ( T o. T ) = T $.
    $( An idempotent Hermitian operator generates a closed subspace.  Part of
       proof of Theorem of [AkhiezerGlazman] p. 64.  (The proof was shortened
       by Mario Carneiro, 19-May-2014.) $)
    hmopidmchi $p |- ran T e. CH $=
      ( vf vx vy vk wcel cn cv ax-mp cfv cmv co c0v wceq cvv chil a1i wb crn wf
      cch csh chli wbr wa wal cho clo hmoplin rnelshi csn cxp cno ccom copn cha
      eqid hilmet methausi ovex ax-hv0cl cmpt clm ctop cme opntop cmap cres cva
      wi csm hhims hhlm resss eqsstri ssbri adantl cuni hilmetba uniopn2 lnopfi
      cop ccn feqmptd cco cbo hmopbdoptHIL lnopcnbd mpbir hhcno eleqtri cnmptid
      syl6eqelr ctx cnv hhnv hhvs vmcn cnmpt12f vex lmcn wral wss simpl sylancl
      shssii fss ffvelrn sylan fveq2 id oveq12d fvmpt syl wfn ffn eqeq12d ralrn
      fveq1i hocoi syl5reqr mprgbir adantlr rcla4cv mpsyl eqeltrd syl2anc eqtrd
      hvsubeq0 mpbird fvco3 elexi fvconst2 3eqtr4d ralrimiva fnmpt sylancr c1
      mprg fnfco fconst eqfnfv2 hlimveci 3brtr3d cz 1z lmconst syl3anc ffvelrni
      nnuz lmmo mpbid fnfvelrn eqeltrrd gen2 isch2 mpbir2an ) AUAZUCHUUTUDHIUUT
      DJZUBZUVAEJZUEUFZUGZUVCUUTHVLZEUHDUHAAUIHZAUJHZBAUKKZULZUVFDEUVEUVCALZUVC
      UUTUVEUVKUVCMNZOPZUVKUVCPZUVEUVLOIOUMZUNZUOMUPZUQLZQRUVRURHUVEUVQUVRUVQUV
      QUSZUTZUVRUSZVASUVLQHUVEUVKUVCMVBZSORHZUVEVCSZUVEFRFJZALZUWEMNZVDZUVAUPZU
      VCUWHLZUVPUVLUVRVELZUVEUVCUVAUWHUVRUVRQUVRVFHZUVEUVQVGHZUWLUVTUVQUVRUWAVH
      KSZUWNUVDUVAUVCUWKUFUVBUEUWKUVAUVCUEUWKRIVINZVJUWKUVQVKVMWDUOWDZUVRUWPUSZ
      UVQUWPUWQUVSVNZUWAVOUWKUWOVPVQVRVSUVEFUWFUWEMUVRUVRUVRUVRRRUWMRUVRVTPUVTU
      VQUVRRUVQUVSWAUWAWBKZUWNUWSUWNUWNUVEFRUWFVDAUVRUVRWENZUVEFRRARRAUBZUVEAUV
      IWCZSWFAWGUWTAWGHZAWHHZUVGUXDBAWIKUVHUXCUXDTUVIAWJKWKUVQUVRUVSUWAWLWMWOUV
      EFUVRRUWSUWNWNUWNMUVRUVRWPNUVRWENHZUVEUWPWQHUXEUWPUWQWRUVQUWPUVRMUWRUWAUW
      PUWQWSWTKSXAUVCQHUVEEXBZSXCUVEUWIUVPPZGJZUWILZUXHUVPLZPZGIXDZUVEUXKGIUVEU
      XHIHZUGZUXHUVALZUWHLZOUXIUXJUXNUXPUXOALZUXOMNZOUXNUXORHZUXPUXRPUVEIRUVAUB
      ZUXMUXSUVEUVBUUTRXEUXTUVBUVDXFUUTUVJXHIUUTRUVAXIXGZIRUXHUVAXJXKZFUXOUWGUX
      RRUWHUWEUXOPZUWFUXQUWEUXOMUWEUXOAXLZUYCXMZXNUWHUSZUXQUXOMVBXOXPUXNUXROPZU
      XQUXOPZUWFUWEPZFUUTXDZUXNUXOUUTHZUYHUYJUVKALZUVKPZERARXQZUYJUYMERXDTUXAUY
      NUXBRRAXRKZUYIUYMFERAUWEUVKPZUWFUYLUWEUVKUWEUVKAXLUYPXMXSXTKUVCRHZUVKUVCA
      AUPZLUYLUVCUYRACYAUVCAAUXBUXBYBYCYDUVBUXMUYKUVDIUUTUXHUVAXJYEUYIUYHFUXOUU
      TUYCUWFUXQUWEUXOUYDUYEXSYFYGZUXNUXQRHUXSUYGUYHTUXNUXQUXORUYSUYBYHUYBUXQUX
      OYKYIYLYJUVBUXMUXIUXPPUVDIUUTUXHUWHUVAYMYEUXMUXJOPUVEIOUXHORVCYNZYOVSYPYQ
      UVEUWIIXQZUVPIXQZUXGUXLTUVEUWHRXQZUXTVUAUWGQHZVUCFRFRUWGUWHQUYFYRVUDUWERH
      UWFUWEMVBSUUAUYARIUWHUVAUUBYSIUVOUVPUBVUBIOUYTUUCIUVOUVPXRKGIUWIUVPUUDXGY
      LUVEUYQUWJUVLPUVDUYQUVBUVCUVAUXFUUEVSZFUVCUWGUVLRUWHUWEUVCPZUWFUVKUWEUVCM
      UWEUVCAXLVUFXMXNUYFUWBXOXPUUFUVEUWLUWCYTUUGHZUVPOUWKUFUWNUWDVUGUVEUUHSOUV
      RYTRIUWSUULUUIUUJUUMUVEUVKRHZUYQUVMUVNTUVEUYQVUHVUERRUVCAUXBUUKXPVUEUVKUV
      CYKYIUUNUVEUYNUYQUVKUUTHUYOVUERUVCAUUOYSUUPUUQEDUUTUURUUS $.
      $( [19-May-2014] $) $( [21-Apr-2006] $)

    $( An idempotent Hermitian operator is a projection operator.  Theorem 26.4
       of [Halmos] p. 44.  (Halmos seems to omit the proof that ` H ` is a
       closed subspace, which is not trivial as ~ hmopidmchi shows.)  (Revised
       by Mario Carneiro, 19-May-2014.) $)
    hmopidmpji $p |- T = ( proj ` ran T ) $=
      ( vx vz vy cfv wceq cv chil wfn wral wb wcel ax-mp csp cc0 syl2anc cmin
      co crn cpj wf cho clo hmoplin lnopfi ffn hmopidmchi pjfni eqfnfv2 cmv cva
      mp2an cort fnfvelrn mpan id ffvelrni hvsubcl wa simpl syl his2sub syl3anc
      adantl hmop mp3an1 sylan2 ccom fveq1i hocoi syl5reqr oveq2d cc hicl subid
      eqtr3d 3eqtrd ralrimiva oveq2 eqeq1d ralrn sylibr wss chssii ocel pjcompi
      sylanbrc hvpncan3 fveq2d mprgbir ) AAUAZUBGZHZDIZAGZWPWNGZHZDJAJKZWNJKWOW
      SDJLMJJAUCWTAAUDNZAUENBAUFOUGZJJAUHOZWMABCUIZUJDJAWNUKUNWPJNZWQWPWQULTZUM
      TZWNGZWQWRXEWQWMNZXFWMUOGNZXHWQHWTXEXIXCJWPAUPUQXEXFJNZXFEIZPTZQHZEWMLZXJ
      XEXEWQJNZXKXEURZJJWPAXBUSZWPWQUTRXEXFFIZAGZPTZQHZFJLZXOXEYBFJXEXSJNZVAZYA
      WPXTPTZWQXTPTZSTZYFYFSTZQYEXEXPXTJNZYAYHHXEYDVBZYEXEXPYKXRVCYDYJXEJJXSAXB
      USZVFWPWQXTVDVEYEYGYFYFSYEWPXTAGZPTZYGYFYDXEYJYNYGHZYLXAXEYJYOBWPXTAVGVHV
      IYEYMXTWPPYDYMXTHXEYDXTXSAAVJZGYMXSYPACVKXSAAXBXBVLVMVFVNVRVNYEYFVONZYIQH
      YDXEYJYQYLWPXTVPVIYFVQVCVSVTWTXOYCMXCXNYBEFJAXLXTHXMYAQXLXTXFPWAWBWCOWDWM
      JWEXJXKXOVAMWMXDWFEXFWMWGOWIWQXFWMXDWHRXEXGWPWNXEXPXEXGWPHXRXQWQWPWJRWKVR
      WL $.
      $( [19-May-2014] $) $( [22-Apr-2006] $)
  $}

  ${
    $d x T $.
    $( An idempotent Hermitian operator generates a closed subspace.  Part of
       proof of Theorem of [AkhiezerGlazman] p. 64. $)
    hmopidmch $p |- ( ( T e. HrmOp /\ ( T o. T ) = T ) -> ran T e. CH ) $=
      ( cho wcel ccom wceq wa crn cch chio rneq eleq1d eleq1 id coeq12d eqeq12d
      cif anbi12d idhmop chil wf1o hoif f1of ax-mp hoid1i pm3.2i elimhyp simpli
      wf simpri hmopidmchi dedth ) ABCZAADZAEZFZAGZHCUOAIPZGZHCAIAUQEZUPURHAUQJ
      KUQUQBCZUQUQDZUQEZUOUTVBFIBCZIIDZIEZFAIUSULUTUNVBAUQBLUSUMVAAUQUSAUQAUQUS
      MZVFNVFOQIUQEZVCUTVEVBIUQBLVGVDVAIUQVGIUQIUQVGMZVHNVHOQVCVERISSITSSIUHUAS
      SIUBUCUDUEUFZUGUTVBVIUIUJUK $.
      $( [19-May-2014] $) $( [24-Apr-2006] $)

    $( An idempotent Hermitian operator is a projection operator.  Theorem 26.4
       of [Halmos] p. 44. $)
    hmopidmpj $p |- ( ( T e. HrmOp /\ ( T o. T ) = T ) ->
                    T = ( proj ` ran T ) ) $=
      ( cho wcel ccom wceq wa crn cpj cfv chio cif id rneq fveq2d eqeq12d eleq1
      coeq12d anbi12d idhmop chil wf1o wf hoif f1of ax-mp hoid1i pm3.2i elimhyp
      simpli simpri hmopidmpji dedth ) ABCZAADZAEZFZAAGZHIZEUPAJKZUSGZHIZEAJAUS
      EZAUSURVAVBLZVBUQUTHAUSMNOUSUSBCZUSUSDZUSEZUPVDVFFJBCZJJDZJEZFAJVBUMVDUOV
      FAUSBPVBUNVEAUSVBAUSAUSVCVCQVCORJUSEZVGVDVIVFJUSBPVJVHVEJUSVJJUSJUSVJLZVK
      QVKORVGVISJTTJUATTJUBUCTTJUDUEUFUGUHZUIVDVFVLUJUKUL $.
      $( [19-May-2014] $) $( [22-Apr-2006] $)
  $}

  ${
    $d x H $.  $d x S $.  $d x T $.
    pjsdi.1 $e |- H e. CH $.
    pjsdi.2 $e |- S : ~H --> ~H $.
    pjsdi.3 $e |- T : ~H --> ~H $.
    $( Distributive law for Hilbert space operator sum. $)
    pjsdii $p |- ( ( proj ` H ) o. ( S +op T ) ) =
                ( ( ( proj ` H ) o. S ) +op ( ( proj ` H ) o. T ) ) $=
      ( vx cfv chos co ccom wceq chil wcel cva ffvelrni wf hosval hocoi hocofi
      cpj wral pjaddi syl2anc mp3an12 fveq2d pjfi oveq12d 3eqtr4d hoaddcli rgen
      cv hoeqi mpbi ) GULZCUAHZABIJZKZHZUOUPAKZUPBKZIJZHZLZGMUBURVBLVDGMUOMNZUO
      UQHZUPHZUOUTHZUOVAHZOJZUSVCVEUOAHZUOBHZOJZUPHZVKUPHZVLUPHZOJZVGVJVEVKMNVL
      MNVNVQLMMUOAEPMMUOBFPVKVLCDUCUDVEVFVMUPMMAQMMBQVEVFVMLEFUOABRUEUFVEVHVOVI
      VPOUOUPACDUGZESUOUPBVRFSUHUIUOUPUQVRABEFUJZSMMUTQMMVAQVEVCVJLUPAVRETZUPBV
      RFTZUOUTVARUEUIUKGURVBUPUQVRVSTUTVAVTWAUJUMUN $.
      $( [12-Nov-2000] $)

    $( Distributive law for Hilbert space operator difference. $)
    pjddii $p |- ( ( proj ` H ) o. ( S -op T ) ) =
                ( ( ( proj ` H ) o. S ) -op ( ( proj ` H ) o. T ) ) $=
      ( vx cfv chod co ccom wceq chil wcel cmv ffvelrni wf hodval hocoi hocofi
      cpj wral pjsubi syl2anc mp3an12 fveq2d pjfi oveq12d 3eqtr4d hosubcli rgen
      cv hoeqi mpbi ) GULZCUAHZABIJZKZHZUOUPAKZUPBKZIJZHZLZGMUBURVBLVDGMUOMNZUO
      UQHZUPHZUOUTHZUOVAHZOJZUSVCVEUOAHZUOBHZOJZUPHZVKUPHZVLUPHZOJZVGVJVEVKMNVL
      MNVNVQLMMUOAEPMMUOBFPVKVLCDUCUDVEVFVMUPMMAQMMBQVEVFVMLEFUOABRUEUFVEVHVOVI
      VPOUOUPACDUGZESUOUPBVRFSUHUIUOUPUQVRABEFUJZSMMUTQMMVAQVEVCVJLUPAVRETZUPBV
      RFTZUOUTVARUEUIUKGURVBUPUQVRVSTUTVAVTWAUJUMUN $.
      $( [24-Nov-2000] $)
  $}

  ${
    pjsdi2.1 $e |- H e. CH $.
    pjsdi2.2 $e |- R : ~H --> ~H $.
    pjsdi2.3 $e |- S : ~H --> ~H $.
    pjsdi2.4 $e |- T : ~H --> ~H $.
    $( Chained distributive law for Hilbert space operator difference. $)
    pjsdi2i $p |- ( ( R o. ( S +op T ) ) =
                ( ( R o. S ) +op ( R o. T ) ) ->
           ( ( ( proj ` H ) o. R ) o. ( S +op T ) ) =
     ( ( ( ( proj ` H ) o. R ) o. S ) +op ( ( ( proj ` H ) o. R ) o. T ) ) ) $=
      ( chos co ccom wceq cpj cfv coeq2 hocofi pjsdii syl6eq coass oveq12i
      3eqtr4g ) ABCIJZKZABKZACKZIJZLZDMNZUCKZUHUDKZUHUEKZIJZUHAKZUBKUMBKZUMCKZI
      JUGUIUHUFKULUCUFUHOUDUEDEABFGPACFHPQRUHAUBSUNUJUOUKIUHABSUHACSTUA $.
      $( [30-Nov-2000] $)
  $}

  ${
    $d x y H $.  $d x y G $.
    pjco.1 $e |- G e. CH $.
    pjco.2 $e |- H e. CH $.
    $( Composition of projections. $)
    pjcoi $p |- ( A e. ~H -> ( ( ( proj ` G ) o. ( proj ` H ) ) ` A ) =
               ( ( proj ` G ) ` ( ( proj ` H ) ` A ) ) ) $=
      ( cpj cfv pjfi hocoi ) ABFGCFGBDHCEHI $.
      $( [16-Aug-2000] $)

    $( Closure of composition of projections. $)
    pjcocli $p |- ( A e. ~H ->
                 ( ( ( proj ` G ) o. ( proj ` H ) ) ` A ) e. G ) $=
      ( chil wcel cpj cfv ccom pjcoi pjhcli pjcli syl eqeltrd ) AFGZABHIZCHIZJI
      ARIZQIZBABCDEKPSFGTBGACELSBDMNO $.
      $( [29-Nov-2000] $)

    $( Closure of composition of projections. $)
    pjcohcli $p |- ( A e. ~H ->
                 ( ( ( proj ` G ) o. ( proj ` H ) ) ` A ) e. ~H ) $=
      ( cpj cfv pjfi hococli ) ABFGCFGBDHCEHI $.
      $( [7-Oct-2000] $)

    $( Adjoint of composition of projections.  Special case of Theorem
       3.11(viii) of [Beran] p. 106. $)
    pjadjcoi $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( ( ( proj ` G ) o. ( proj ` H ) ) ` A ) .ih B ) =
                  ( A .ih ( ( ( proj ` H ) o. ( proj ` G ) ) ` B ) ) ) $=
      ( chil wcel wa cpj cfv csp co ccom wceq pjhcli pjadji sylan sylan2 pjcoi
      eqtrd oveq1d adantr oveq2d adantl 3eqtr4d ) AGHZBGHZIZADJKZKZCJKZKZBLMZAB
      ULKZUJKZLMZAULUJNKZBLMZABUJULNKZLMZUIUNUKUOLMZUQUGUKGHUHUNVBOADFPUKBCEQRU
      HUGUOGHVBUQOBCEPAUODFQSUAUGUSUNOUHUGURUMBLACDEFTUBUCUHVAUQOUGUHUTUPALBDCF
      ETUDUEUF $.
      $( [6-Oct-2000] $)

    $( Functionality of composition of projections. $)
    pjcofni $p |- ( ( proj ` G ) o. ( proj ` H ) ) Fn ~H $=
      ( cpj cfv pjfi hocofni ) AEFBEFACGBDGH $.
      $( [1-Oct-2000] $)

    $( Subset relationship for projections.  Theorem 4.5(i)<->(iii) of [Beran]
       p. 112. $)
    pjss1coi $p |- ( G C_ H <->
                 ( ( proj ` H ) o. ( proj ` G ) ) = ( proj ` G ) ) $=
      ( vx wss cpj cfv ccom wceq cv chil wral wcel pjcoi adantl pjfi crn pjrni
      wa cch pjcli ssel2 sylan2 pjid sylancr eqtrd ralrimiva hocofi hoeqi sylib
      rneq rncoss sseq1 mpbii syl 3sstr3g impbii ) ABFZBGHZAGHZIZVAJZUSEKZVBHZV
      DVAHZJZELMVCUSVGELUSVDLNZTZVEVFUTHZVFVHVEVJJUSVDBADCOPVIBUANVFBNZVJVFJDVH
      USVFANVKVDACUBABVFUCUDVFBUEUFUGUHEVBVAUTVABDQACQZUIVLUJUKVCVARZUTRZABVCVB
      RZVMJZVMVNFZVBVAULVPVOVNFVQUTVAUMVOVMVNUNUOUPACSBDSUQUR $.
      $( [1-Oct-2000] $)

    $( Subset relationship for projections.  Theorem 4.5(i)<->(ii) of [Beran]
       p. 112. $)
    pjss2coi $p |- ( G C_ H <->
                 ( ( proj ` G ) o. ( proj ` H ) ) = ( proj ` G ) ) $=
      ( vx vy cpj cfv ccom wceq cv chil wral wcel wa wi c0v fveq2 csp co adantl
      wss pjcoi fveq2d eqeq12d imbi2d ax-hv0cl elimel pjss2i dedth impcom eqtrd
      ralrimiva pjfi hocofi hoeqi sylib oveq2d ad2antlr pjadjcoi adantlr pjadji
      cif fveq1 3eqtr4d exp31 ralrimdv wb pjcohcli pjhcli hial2eq syl2anc com12
      sylibd ralrimiv pjss1coi sylibr impbii ) ABUBZAGHZBGHZIZVTJZVSEKZWBHZWDVT
      HZJZELMWCVSWGELVSWDLNZOWEWDWAHZVTHZWFWHWEWJJVSWDABCDUCUAWHVSWJWFJZWHVSWKP
      VSWHWDQVCZWAHZVTHZWLVTHZJZPWDQWDWLJZWKWPVSWQWJWNWFWOWQWIWMVTWDWLWARUDWDWL
      VTRUEUFWLBACWDQLUGUHDUIUJUKULUMEWBVTVTWAACUNZBDUNZUOWRUPUQWCWAVTIZVTJZVSW
      CWDWTHZWFJZELMXAWCXCELWHWCXCWHWCXBFKZSTZWFXDSTZJZFLMZXCWHWCXGFLWHWCXDLNZX
      GWHWCOXIOWDXDWBHZSTZWDXDVTHZSTZXEXFWCXKXMJWHXIWCXJXLWDSXDWBVTVDURUSWHXIXE
      XKJWCWDXDBADCUTVAWHXIXFXMJWCWDXDACVBVAVEVFVGWHXBLNWFLNXHXCVHWDBADCVIWDACV
      JFXBWFVKVLVNVMVOEWTVTWAVTWSWRUOWRUPUQABCDVPVQVR $.
      $( [7-Oct-2000] $)

    $( Projection meet property.  Remark in [Kalmbach] p. 66.  Also Theorem
       4.5(i)->(iv) of [Beran] p. 112. $)
    pjssmi $p |- ( A e. ~H -> ( H C_ G ->
               ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) )
                = ( ( proj ` ( G i^i ( _|_ ` H ) ) ) ` A ) ) ) $=
      ( chil wcel wss cpj cfv cmv co cort cin wceq wi c0v cif fveq2 oveq12d
      eqeq12d imbi2d ax-hv0cl elimel pjssmii dedth ) AFGZCBHZABIJZJZACIJZJZKLZA
      BCMJNIJZJZOZPUHUGAQRZUIJZUQUKJZKLZUQUNJZOZPAQAUQOZUPVBUHVCUMUTUOVAVCUJURU
      LUSKAUQUISAUQUKSTAUQUNSUAUBUQBCEAQFUCUDDUEUF $.
      $( [26-Sep-2001] $)

    $( Theorem 4.5(iv)->(v) of [Beran] p. 112. $)
    pjssge0i $p |- ( A e. ~H ->
                 ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) )
                  = ( ( proj ` ( G i^i ( _|_ ` H ) ) ) ` A ) ->
       0 <_ ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) .ih A ) ) ) $=
      ( chil wcel cpj cfv cmv co wceq cc0 csp cle wbr wi c0v fveq2 oveq12d cort
      cin cif eqeq12d id breq2d imbi12d ax-hv0cl elimel pjssge0ii dedth ) AFGZA
      BHIZIZACHIZIZJKZABCUAIUBHIZIZLZMUQANKZOPZQULARUCZUMIZVCUOIZJKZVCURIZLZMVF
      VCNKZOPZQARAVCLZUTVHVBVJVKUQVFUSVGVKUNVDUPVEJAVCUMSAVCUOSTZAVCURSUDVKVAVI
      MOVKUQVFAVCNVLVKUETUFUGVCBCEARFUHUIDUJUK $.
      $( [26-Sep-2001] $)

    $( Theorem 4.5(v)<->(vi) of [Beran] p. 112. $)
    pjdifnormi $p |- ( A e. ~H -> ( 0 <_
                ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) .ih A ) <->
    ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` ( ( proj ` G ) ` A ) ) ) ) $=
      ( chil cc0 cpj cfv cmv co csp cle wbr cno wb c0v fveq2 oveq12d fveq2d cif
      wcel wceq id breq2d breq12d bibi12d ax-hv0cl elimel pjdifnormii dedth ) A
      FUBZGABHIZIZACHIZIZJKZALKZMNZUPOIZUNOIZMNZPGULAQUAZUMIZVCUOIZJKZVCLKZMNZV
      EOIZVDOIZMNZPAQAVCUCZUSVHVBVKVLURVGGMVLUQVFAVCLVLUNVDUPVEJAVCUMRZAVCUORZS
      VLUDSUEVLUTVIVAVJMVLUPVEOVNTVLUNVDOVMTUFUGVCBCEAQFUHUIDUJUK $.
      $( [26-Sep-2001] $)

    $( Theorem 4.5(i)<->(vi) of [Beran] p. 112. $)
    pjnormssi $p |- ( G C_ H <-> A. x e. ~H ( normh ` ( ( proj ` G ) ` x ) ) <_
                   ( normh ` ( ( proj ` H ) ` x ) ) ) $=
      ( wss cpj cfv cno cle wbr chil wcel cc0 co wceq wi wa syl wb wral cmv csp
      cv cort cin pjssmi pjssge0i syld pjdifnormi sylibd com12 ralrimiv choccli
      wal cheli breq2 biimpac pjhcli normge0 normcl 0re biimprd sylancl sylan2i
      cr letri3 anabsi6 sylan2 expr c0v norm-i pjoc2 mpan bitr4d adantr 3imtr3d
      cch a2i syl5 pm2.43d alimi df-ral dfss2 3imtr4i chsscon3i sylibr impbii
      ex ) BCFZAUDZBGHHZIHZWKCGHHZIHZJKZALUAZWJWPALWKLMZWJWPWRWJNWNWLUBOZWKUCOJ
      KZWPWRWJWSWKCBUEHZUFGHHPWTWKCBEDUGWKCBEDUHUIWKCBEDUJUKULUMWQCUEHZXAFZWJWR
      WPQZAUOWKXBMZWKXAMZQZAUOWQXCXDXGAXDXEXFXEWRXDXGWKXBCEUNUPWRWPXGWRWPXGWRWP
      RWONPZWMNPZXEXFWRWPXHXIWPXHRWRWMNJKZXIXHWPXJWONWMJUQURWRXJXIWRWRXJNWMJKZX
      IWRWLLMZXKWKBDUSZWLUTSWRWMVFMZNVFMZXJXKRZXIQWRXLXNXMWLVASVBXNXORXIXPWMNVG
      VCVDVEVHVIVJWRXHXETWPWRXHWNVKPZXEWRWNLMXHXQTWKCEUSWNVLSCVRMWRXEXQTEWKCVMV
      NVOVPWRXIXFTWPWRXIWLVKPZXFWRXLXIXRTXMWLVLSBVRMWRXFXRTDWKBVMVNVOVPVQWIVSVT
      WAWBWPALWCAXBXAWDWEBCDEWFWGWH $.
      $( [26-Sep-2001] $)

    $( Composition of projections of orthogonal subspaces.  Part (i)->(iia) of
       Theorem 27.4 of [Halmos] p. 45. $)
    pjorthcoi $p |- ( G C_ ( _|_ ` H ) ->
                 ( ( proj ` G ) o. ( proj ` H ) ) = 0hop ) $=
      ( vx cort cfv wss cv cpj ccom ch0o wceq chil wral wcel wa c0v adantl pjfi
      pjcli wi chsscon2i ssel sylbi syl5com cch wb pjhcli sylancr sylibd impcom
      pjoc2 pjcoi ho0val 3eqtr4d ralrimiva hocofi ho0f hoeqi sylib ) ABFGHZEIZA
      JGZBJGZKZGZVCLGZMZENOVFLMVBVIENVBVCNPZQVCVEGZVDGZRVGVHVJVBVLRMZVJVBVKAFGZ
      PZVMVJVKBPZVBVOVCBDUAVBBVNHVPVOUBABCDUCBVNVKUDUEUFVJAUGPVKNPVOVMUHCVCBDUI
      VKAUMUJUKULVJVGVLMVBVCABCDUNSVJVHRMVBVCUOSUPUQEVFLVDVEACTBDTURUSUTVA $.
      $( [24-Dec-2007] $) $( [6-Nov-2000] $)

    $( The projection of orthogonal subspaces is the sum of the projections. $)
    pjscji $p |- ( G C_ ( _|_ ` H ) -> ( proj ` ( G vH H ) ) =
              ( ( proj ` G ) +op ( proj ` H ) ) ) $=
      ( vx cort cfv wss cv chj co cpj chos wceq chil wcel cch mp3an12 wf pjfi
      wa cva pjcjt2 impcom hosval adantl eqtr4d ralrimiva chjcli hoaddcli hoeqi
      wral wi sylib ) ABFGHZEIZABJKZLGZGZUPALGZBLGZMKZGZNZEOULURVBNUOVDEOUOUPOP
      ZUAUSUPUTGUPVAGUBKZVCVEUOUSVFNZAQPBQPVEUOVGUMCDUPBAUCRUDVEVCVFNZUOOOUTSOO
      VASVEVHACTZBDTZUPUTVAUERUFUGUHEURVBUQABCDUITUTVAVIVJUJUKUN $.
      $( [11-Nov-2000] $)

    $( The projection on a subspace sum is the sum of the projections. $)
    pjssumi $p |- ( G C_ ( _|_ ` H ) -> ( proj ` ( G +H H ) ) =
              ( ( proj ` G ) +op ( proj ` H ) ) ) $=
      ( cort cfv wss cph co cpj chj chos osumi fveq2d pjscji eqtrd ) ABEFGZABHI
      ZJFABKIZJFAJFBJFLIQRSJABCDMNABCDOP $.
      $( [11-Nov-2000] $)

    $( Projector ordering can be expressed by the subset relationship between
       their projection subspaces.  (i)<->(iii) of Theorem 29.2 of [Halmos]
       p. 48. $)
    pjssposi $p |- ( A. x e. ~H
       0 <_ ( ( ( ( proj ` H ) -op ( proj ` G ) ) ` x ) .ih x ) <-> G C_ H ) $=
      ( cc0 cpj cfv co csp cle wbr chil wral cno wcel c2 cmin cr syl cv chod wb
      wss cexp pjhcli normcl resqcl 3syl subge0 syl2anc cmv wf wceq pjfi hodval
      mp3an12 oveq1d id his2sub syl3anc pjinormi oveq12d 3eqtrd breq2d syl22anc
      normge0 le2sq 3bitr4d ralbiia pjnormssi bitr4i ) FAUAZCGHZBGHZUBIHZVMJIZK
      LZAMNVMVOHZOHZVMVNHZOHZKLZAMNBCUDVRWCAMVMMPZFWBQUEIZVTQUEIZRIZKLZWFWEKLZV
      RWCWDWESPZWFSPZWHWIUCWDWAMPZWBSPZWJVMCEUFZWAUGZWBUHUIWDVSMPZVTSPZWKVMBDUF
      ZVSUGZVTUHUIWEWFUJUKWDVQWGFKWDVQWAVSULIZVMJIZWAVMJIZVSVMJIZRIZWGWDVPWTVMJ
      MMVNUMMMVOUMWDVPWTUNCEUOBDUOVMVNVOUPUQURWDWLWPWDXAXDUNWNWRWDUSWAVSVMUTVAW
      DXBWEXCWFRVMCEVBVMBDVBVCVDVEWDWQFVTKLZWMFWBKLZWCWIUCWDWPWQWRWSTWDWPXEWRVS
      VGTWDWLWMWNWOTWDWLXFWNWAVGTVTWBVHVFVIVJABCDEVKVL $.
      $( [2-Jun-2006] $)

    $( The definition of projector ordering in [Halmos] p. 42 is equivalent to
       the definition of projector ordering in [Beran] p. 110.  (We will
       usually express projector ordering with the even simpler equivalent
       ` G C_ H ` ; see ~ pjssposi ). $)
    pjordi $p |- ( A. x e. ~H
        0 <_ ( ( ( ( proj ` H ) -op ( proj ` G ) ) ` x ) .ih x ) <->
        ( ( proj ` G ) " ~H ) C_ ( ( proj ` H ) " ~H ) ) $=
      ( cc0 cv cpj cfv chod co csp chil wss cima wfo wceq pjfoi foima ax-mp cle
      wbr wral pjssposi sseq12i bitr4i ) FAGZCHIZBHIZJKIUGLKUAUBAMUCBCNUIMOZUHM
      OZNABCDEUDUJBUKCMBUIPUJBQBDRMBUISTMCUHPUKCQCERMCUHSTUEUF $.
      $( [2-Jun-2006] $)

    $( The projection subspace of the difference between two projectors.  Part
       2 of Theorem 29.3 of [Halmos] p. 48 (shortened with ~ pjssposi ). $)
    pjssdif2i $p |- ( G C_ H <->
      ( ( proj ` H ) -op ( proj ` G ) ) = ( proj ` ( H i^i ( _|_ ` G ) ) ) ) $=
      ( vx cpj cfv co wceq chil wral wa pjfi adantl ralrimiva wfn cc0 csp cle
      wf wss chod cort cin wcel cmv hodval mp3an12 pjssmi impcom eqtrd hosubfni
      cv wb choccli chincli pjfni eqfnfv2 mp2an sylibr wbr pjige0i fveq1 oveq1d
      adantr breqtrrd pjssposi sylib impbii ) ABUAZBFGZAFGZUBHZBAUCGZUDZFGZIZVJ
      EUMZVMGZVRVPGZIZEJKZVQVJWAEJVJVRJUEZLVSVRVKGVRVLGUFHZVTWCVSWDIZVJJJVKTJJV
      LTWCWEBDMZACMZVRVKVLUGUHNWCVJWDVTIVRBADCUIUJUKOVMJPVPJPVQWBUNVKVLWFWGULVO
      BVNDACUOUPZUQEJVMVPURUSUTVQQVSVRRHZSVAZEJKVJVQWJEJVQWCLQVTVRRHZWISWCQWKSV
      AVQVRVOWHVBNVQWIWKIWCVQVSVTVRRVRVMVPVCVDVEVFOEABCDVGVHVI $.
      $( [2-Jun-2006] $)

    $( A necessary and sufficient condition for the difference between two
       projectors to be a projector.  Part 1 of Theorem 29.3 of [Halmos] p. 48
       (shortened with ~ pjssposi ). $)
    pjssdif1i $p |- ( G C_ H <->
                  ( ( proj ` H ) -op ( proj ` G ) ) e. ran proj ) $=
      ( vy vx cpj cfv co wceq wcel cch pjmfn cc0 cv csp cle wbr chil wb wss cin
      chod cort crn pjssdif2i wfn choccli fnfvelrn mp2an eleq1 mpbiri wral wrex
      chincli fvelrnb ax-mp pjige0 fveq1 oveq1d breq2d ad2antlr mpbid ralrimiva
      wa adantlr rexlimiva sylbi pjssposi bitri sylib impbii ) ABUAZBGHAGHUCIZB
      AUDHZUBZGHZJZVNGUEZKZABCDUFZVRVTVRVTVQVSKZGLUGZVPLKWBMBVODACUHUOLVPGUIUJV
      NVQVSUKULVTNEOZVNHZWDPIZQRZESUMZVRVTFOZGHZVNJZFLUNZWHWCVTWLTMFLVNGUPUQWKW
      HFLWILKZWKVEZWGESWNWDSKZVENWDWJHZWDPIZQRZWGWMWOWRWKWDWIURVFWKWRWGTWMWOWKW
      QWFNQWKWPWEWDPWDWJVNUSUTVAVBVCVDVGVHWHVMVREABCDVIWAVJVKVLVJ $.
      $( [2-Jun-2006] $)
  $}

  ${
    $d u v w A $.  $d u v w B $.
    pjima.1 $e |- A e. SH $.
    pjima.2 $e |- B e. CH $.
    $( The image of a projection.  Lemma 5 in Daniel Lehmann, "A presentation
       of Quantum Logic based on an _and then_ connective"
       ~ http://www.arxiv.org/pdf/quant-ph/0701113 p. 20. $)
    pjimai $p |- ( ( proj ` B ) " A ) = ( ( A +H ( _|_ ` B ) ) i^i B ) $=
      ( vu vv vw cfv co cv wcel wa wceq wrex cva chil wb w3a cheli bitr4d sheli
      cpj cima cort cph cin cmv cch pjeq sylancr ibar bicomd sylan9bbr hvsubadd
      choccli 3comr ax-hvcom 3adant2 eqeq1d syl3an eqcom 3bitr4g 3expa rexbidva
      wfn wss pjfni shssii fvelimab mp2an csh chshii pm5.32ri crn imassrn pjrni
      shsel3 sseqtri sseli pm4.71i elin 3bitr4i eqriv ) EBUBHZAUCZABUDHZUEIZBUF
      ZEJZWEKZWIBKZLWIWGKZWKLWJWIWHKWKWJWLWKFJZWDHWIMZFANZWIWMGJZUGIZMZGWFNZFAN
      ZWJWLWKWNWSFAWKWMAKZLZWNWMWIWPOIZMZGWFNZWSXAWNWKXELZWKXEXABUHKWMPKZWNXFQD
      WMACUAZGWMWIBUIUJWKXEXFWKXEUKULUMXBWRXDGWFWKXAWPWFKZWRXDQWKXAXIRWQWIMZXCW
      MMZWRXDWKWIPKZXAXGXIWPPKZXJXKQWIBDSXHWPWFBDUOZSXLXGXMRZXJWPWIOIZWMMZXKXGX
      MXLXJXQQWMWPWIUNUPXOXCXPWMXLXMXCXPMXGWIWPUQURUSTUTWIWQVAWMXCVAVBVCVDTVDWD
      PVEAPVFWJWOQBDVGACVHFPAWIWDVIVJAVKKWFVKKWLWTQCWFXNVLFGAWFWIVQVJVBVMWJWKWE
      BWIWEWDVNBWDAVOBDVPVRVSVTWIWGBWAWBWC $.
      $( [19-May-2014] $) $( [20-Jan-2007] $)
  $}

  ${
    $d x H $.
    pjidmco.1 $e |- H e. CH $.
    $( A projection is idempotent.  Property (ii) of [Beran] p. 109. $)
    pjidmcoi $p |- ( ( proj ` H ) o. ( proj ` H ) ) = ( proj ` H ) $=
      ( wss cpj cfv ccom wceq ssid pjss2coi mpbi ) AACADEZKFKGAHAABBIJ $.
      $( [1-Oct-2000] $)

    $( Composition of projections of a subspace and its orthocomplement. $)
    pjoccoi $p |- ( ( proj ` H ) o. ( proj ` ( _|_ ` H ) ) ) = 0hop $=
      ( chil wss cort cfv ccom ch0o wceq chssii ococss choccli pjorthcoi mp2b
      cpj ) ACDAAEFZEFDAOFPOFGHIABJAKAPBABLMN $.
      $( [5-Jul-2008] $) $( [14-Nov-2000] $)

    $( Subspace sum of projection and projection of orthocomplement. $)
    pjtoi $p |- ( ( proj ` H ) +op ( proj ` ( _|_ ` H ) ) ) = ( proj ` ~H ) $=
      ( vx cv cpj cfv cort chos co chil wceq wral wcel cva axpjpj mpan pjch1 wf
      cch pjfi choccli hosval mp3an12 3eqtr4rd rgen hoaddcli helch hoeqi mpbi )
      CDZAEFZAGFZEFZHIZFZUJJEFZFZKZCJLUNUPKURCJUJJMZUJUJUKFUJUMFNIZUQUOASMUSUJU
      TKBUJAOPUJQJJUKRJJUMRUSUOUTKABTZULABUATZUJUKUMUBUCUDUECUNUPUKUMVAVBUFJUGT
      UHUI $.
      $( [16-Nov-2000] $)

    $( Projection of orthocomplement.  First part of Theorem 27.3 of [Halmos]
       p. 45. $)
    pjoci $p |- ( ( proj ` ~H ) -op ( proj ` H ) ) = ( proj ` ( _|_ ` H ) ) $=
      ( chil cpj cfv chod cort wceq chos pjtoi helch pjfi choccli hodsi mpbir
      co ) CDEZADEZFPAGEZDEZHRTIPQHABJQRTCKLABLSABMLNO $.
      $( [26-Nov-2000] $)
  $}

  $( A projection operator is idempotent.  Property (ii) of [Beran] p. 109. $)
  pjidmco $p |- ( H e. CH ->
                 ( ( proj ` H ) o. ( proj ` H ) ) = ( proj ` H ) ) $=
    ( cch wcel cpj cfv ccom wceq c0h cif coeq12d eqeq12d h0elch elimel pjidmcoi
    fveq2 dedth ) ABCZADEZRFZRGQAHIZDEZUAFZUAGAHATGZSUBRUAUCRUARUAATDOZUDJUDKTA
    HBLMNP $.
    $( [24-Apr-2006] $)

  ${
    $d h t u x T $.
    $( Definition of projection operator in [Hughes] p. 47, except that we do
       not need linearity to be explicit by virtue of ~ hmoplin .  (Revised by
       Mario Carneiro, 19-May-2014.) $)
    dfpjop $p |- ( T e. ran proj <-> ( T e. HrmOp /\ ( T o. T ) = T ) ) $=
      ( vx cpj crn wcel cho ccom wceq wa cv cfv cch wrex wb pjmfn fvelrnb ax-mp
      wfn pjhmop pjidmco eleq1 coeq12d eqeq12d anbi12d syl5ibcom rexlimiv sylbi
      jca id hmopidmpj hmopidmch fnfvelrn sylancr eqeltrd impbii ) ACDZEZAFEZAA
      GZAHZIZUQBJZCKZAHZBLMZVACLRZUQVENOBLACPQVDVABLVBLEZVCFEZVCVCGZVCHZIVDVAVG
      VHVJVBSVBTUHVDVHURVJUTVCAFUAVDVIUSVCAVDVCAVCAVDUIZVKUBVKUCUDUEUFUGVAAADZC
      KZUPAUJVAVFVLLEVMUPEOAUKLVLCULUMUNUO $.
      $( [24-Apr-2006] $)

    $( Two ways to express the set of all projection operators.  (The proof was
       shortened by Mario Carneiro, 19-May-2014.) $)
    pjhmopidm $p |- ran proj = { t e. HrmOp | ( t o. t ) = t } $=
      ( cpj crn cv cho wcel ccom wceq wa cab crab dfpjop abbi2i df-rab eqtr4i )
      BCZADZEFQQGQHZIZAJRAEKSAPQLMRAENO $.
      $( [19-May-2014] $) $( [24-Apr-2006] $)
  $}

  $( A projection operator is idempotent.  Part of Theorem 26.1 of [Halmos]
     p. 43. $)
  elpjidm $p |- ( T e. ran proj -> ( T o. T ) = T ) $=
    ( cpj crn wcel cho ccom wceq dfpjop simprbi ) ABCDAEDAAFAGAHI $.
    $( [2-Dec-2006] $) $( [24-Apr-2006] $)

  $( A projection operator is Hermitian.  Part of Theorem 26.1 of [Halmos]
     p. 43. $)
  elpjhmop $p |- ( T e. ran proj -> T e. HrmOp ) $=
    ( cpj crn wcel cho ccom wceq dfpjop simplbi ) ABCDAEDAAFAGAHI $.
    $( [24-Apr-2006] $)

  $( A projector is a positive operator. $)
  0leopj $p |- ( T e. ran proj -> 0hop <_op T ) $=
    ( cpj crn wcel ch0o ccom cleo cho wbr elpjhmop leopsq syl elpjidm breqtrd )
    ABCDZEAAFZAGOAHDEPGIAJAKLAMN $.
    $( [27-Sep-2008] $)

  $( A projector is self-adjoint.  Property (i) of [Beran] p. 109. $)
  pjadj2 $p |- ( T e. ran proj -> ( adjh ` T ) = T ) $=
    ( cpj crn wcel cho cado cfv wceq elpjhmop hmopadj syl ) ABCDAEDAFGAHAIAJK
    $.
    $( [3-Jun-2006] $)

  $( A projector is self-adjoint.  Property (i) of [Beran] p. 109. $)
  pjadj3 $p |- ( H e. CH -> ( adjh ` ( proj ` H ) ) = ( proj ` H ) ) $=
    ( cch wcel cpj cfv crn cado wceq wfn pjmfn fnfvelrn mpan pjadj2 syl ) ABCZA
    DEZDFCZPGEPHDBIOQJBADKLPMN $.
    $( [20-Feb-2006] $)

  ${
    $d x y T $.
    $( Reconstruction of the subspace of a projection operator.  Part of
       Theorem 26.2 of [Halmos] p. 44. $)
    elpjch $p |- ( T e. ran proj -> ( ran T e. CH /\
                T = ( proj ` ran T ) ) ) $=
      ( cpj crn wcel cho ccom wceq cch cfv dfpjop hmopidmch hmopidmpj jca sylbi
      wa ) ABCDAEDAAFAGOZACZHDZAQBIGZOAJPRSAKALMN $.
      $( [24-Apr-2006] $)

    $( Reconstruction of the subspace of a projection operator.  (Revised by
       Mario Carneiro, 19-May-2014.) $)
    elpjrn $p |- ( T e. ran proj -> ran T = { x e. ~H | ( T ` x ) = x } ) $=
      ( vy cpj crn wcel cv chil cfv wceq wa cab cch wss elpjch simpld syl sylan
      crab syl5ibcom chss sseld wrex wfn wb cho elpjhmop hmopf ffn fvelrnb ccom
      wf fvco3 elpjidm adantr fveq1d eqtr3d fveq2 eqeq12d rexlimdva sylbid jcad
      id fnfvelrn eleq1 expimpd impbid abbi2dv df-rab syl6eqr ) BDEFZBEZAGZHFZV
      MBIZVMJZKZALVPAHSVKVQAVLVKVMVLFZVQVKVRVNVPVKVLHVMVKVLMFZVLHNVKVSBVLDIJBOP
      VLUAQUBVKVRCGZBIZVMJZCHUCZVPVKBHUDZVRWCUEVKHHBULZWDVKBUFFWEBUGBUHQZHHBUIQ
      ZCHVMBUJQVKWBVPCHVKVTHFZKZWABIZWAJWBVPWIVTBBUKZIZWJWAVKWEWHWLWJJWFHHVTBBU
      MRWIVTWKBVKWKBJWHBUNUOUPUQWBWJVOWAVMWAVMBURWBVCUSTUTVAVBVKVNVPVRVKVNKVOVL
      FZVPVRVKWDVNWMWGHVMBVDRVOVMVLVETVFVGVHVPAHVIVJ $.
      $( [19-May-2014] $) $( [24-Apr-2006] $)
  $}

  ${
    $d x H $.  $d x S $.  $d x T $.
    pjinvar.1 $e |- S : ~H --> ~H $.
    pjinvar.2 $e |- H e. CH $.
    pjinvar.3 $e |- T = ( proj ` H ) $.
    $( A closed subspace ` H ` with projection ` T ` is invariant under an
       operator ` S ` iff ` S T = T S T ` .  Theorem 27.1 of [Halmos] p. 45. $)
    pjinvari $p |- ( ( S o. T ) : ~H --> H <->
                  ( S o. T ) = ( T o. ( S o. T ) ) ) $=
      ( vx chil ccom wf wceq cv cfv wral wcel wa cpj mp2an wfn hocofni cch pjid
      fveq1i ffvelrn sylancr syl5req wss wfo pjfoi fof ax-mp feq1i mpbir chssii
      fss hocofi hocoi adantl eqtr4d ralrimiva eqfnfv2 sylibr fco mpbiri impbii
      wb feq1 ) HCABIZJZVHBVHIZKZVIGLZVHMZVLVJMZKZGHNZVKVIVOGHVIVLHOZPZVMVMBMZV
      NVRVSVMCQMZMZVMVMBVTFUCVRCUAOVMCOWAVMKEHCVLVHUDVMCUBUEUFVQVNVSKVIVLBVHHCB
      JZCHUGHHBJWBHCVTJZHCVTUHWCCEUIHCVTUJUKHCBVTFULUMZCEUNHCHBUORZABDWEUPZUQUR
      USUTVHHSVJHSVKVPVFABDWETBVHWEWFTGHVHVJVARVBVKVIHCVJJZWBHHVHJWGWDWFHHCBVHV
      CRHCVHVJVGVDVE $.
      $( [24-Apr-2006] $)
  $}

  ${
    pjin1.1 $e |- G e. CH $.
    pjin1.2 $e |- H e. CH $.
    $( Lemma for Theorem 1.22 of Mittelstaedt, p. 20. $)
    pjin1i $p |- ( proj ` ( G i^i H ) ) =
                ( ( proj ` G ) o. ( proj ` ( G i^i H ) ) ) $=
      ( cpj cfv cin ccom wss wceq inss1 chincli pjss1coi mpbi eqcomi ) AEFABGZE
      FZHZQPAIRQJABKPAABCDLCMNO $.
      $( [22-Apr-2001] $)

    $( Lemma for Theorem 1.22 of Mittelstaedt, p. 20. $)
    pjin2i $p |- ( ( ( proj ` G ) = ( ( proj ` G ) o. ( proj ` H ) ) /\
                    ( proj ` H ) = ( ( proj ` H ) o. ( proj ` G ) ) ) <->
                ( proj ` G ) = ( proj ` H ) ) $=
      ( cpj cfv ccom wceq wa wss eqss pjss2coi eqcom bitri anbi12i bitr2i fveq2
      sylbi pjidmcoi coeq2 syl5eqr syl6req jca impbii ) AEFZUEBEFZGZHZUFUFUEGZH
      ZIZUEUFHZUKABHZULUMABJZBAJZIUKABKUNUHUOUJUNUGUEHUHABCDLUGUEMNUOUIUFHUJBAD
      CLUIUFMNOPABEQRULUHUJULUEUEUEGUGACSUEUFUETUAULUIUFUFGUFUEUFUFTBDSUBUCUD
      $.
      $( [22-Apr-2001] $)
  $}

  ${
    pjin3.1 $e |- F e. CH $.
    pjin3.2 $e |- G e. CH $.
    pjin3.3 $e |- H e. CH $.
    $( Lemma for Theorem 1.22 of Mittelstaedt, p. 20. $)
    pjin3i $p |- ( ( ( proj ` F ) = ( ( proj ` F ) o. ( proj ` G ) ) /\
                    ( proj ` F ) = ( ( proj ` F ) o. ( proj ` H ) ) ) <->
                ( proj ` F ) = ( ( proj ` F ) o. ( proj ` ( G i^i H ) ) ) ) $=
      ( wss cin cpj ccom wceq ssin pjss2coi eqcom bitri anbi12i chincli 3bitr3i
      wa cfv ) ABGZACGZSABCHZGZAITZUEBITJZKZUEUECITJZKZSUEUEUCITJZKZABCLUAUGUBU
      IUAUFUEKUGABDEMUFUENOUBUHUEKUIACDFMUHUENOPUDUJUEKUKAUCDBCEFQMUJUENOR $.
      $( [22-Apr-2001] $)
  $}

  ${
    $d x y G $.  $d x y H $.
    pjclem1.1 $e |- G e. CH $.
    pjclem1.2 $e |- H e. CH $.
    $( Lemma for projection commutation theorem. $)
    pjclem1 $p |- ( G C_H H ->
       ( ( proj ` G ) o. ( proj ` H ) ) = ( proj ` ( G i^i H ) ) ) $=
      ( cpj cfv ccom cort chos cin ch0o chj wceq sylbi wss chincli ax-mp eqeq2i
      co pjfi ccm cmbri fveq2 inss2 choccli chub2i chdmm3i sseqtr4i sstri coeq2
      wbr pjscji pjsdii pjss1coi mpbi pjorthcoi oveq12i hoaddid1i 3eqtri syl6eq
      inss1 syl cmcm3i bitri chdmm4i chub1i chdmm2i oveq12d pjtoi coeq2i df-iop
      chil chio hoid1i eqtr3i 3eqtr3ri hocofi eqtr2i 3eqtr3g ) ABUAUKZAEFZBEFZW
      AGZGZWAWBAHFZEFZGZGZISZABJZEFZKISZWAWBGZWKVTWDWKWHKIVTWAWJABHFZJZLSZEFZMZ
      WDWKMZVTAWPMWRABCDUBAWPEUCNWRWAWKWOEFZISZMZWSWQXAWAWJWOHFZOWQXAMWJBXCABUD
      ZBWEBLSXCBWEDACUEZUFABCDUGUHZUIWJWOABCDPZAWNCBDUEZPZULQRXBWCWBXAGZMZWSWAX
      AWBUJXKWCWKMZWSXJWKWCXJWBWKGZWBWTGZISWLWKWKWTBDWJXGTZWOXITUMXMWKXNKIWJBOX
      MWKMXDWJBXGDUNUOBXCOXNKMXFBWODXIUPQUQWKXOURZUSRXLWDWAWKGZWKWCWKWAUJWJAOXQ
      WKMABVAWJAXGCUNUOUTNVBNVBVTWEWEBJZWEWNJZLSZMZWHKMZVTWEBUAUKYAABCDVCWEBXED
      UBVDYAWFXTEFZMZYBWEXTEUCYDWFXREFZXSEFZISZMZYBYCYGWFXRXSHFZOYCYGMXRBYIWEBU
      DZBABLSYIBADCUFABCDVEUHZUIXRXSWEBXEDPZWEWNXEXHPZULQRYHWGWBYGGZMZYBWFYGWBU
      JYOWGYEMZYBYNYEWGYNWBYEGZWBYFGZISYEKISYEYEYFBDXRYLTZXSYMTUMYQYEYRKIXRBOYQ
      YEMYJXRBYLDUNUOBYIOYRKMYKBXSDYMUPQUQYEYSURUSRYPWHWAYEGZKWGYEWAUJAXRHFZOYT
      KMAAWNLSUUAAWNCXHVFABCDVGUHAXRCYLUPQUTNVBNVBNVHWMWAWCWGISZGWIWBUUBWAWBWAW
      FISZGWBVLEFZGZUUBWBUUCUUDWBACVIVJWAWFBDACTZWEXETZUMWBVMGUUEWBVMUUDWBVKVJW
      BBDTZVNVOVPVJWCWGACWBWAUUHUUFVQWBWFUUHUUGVQUMVRXPVS $.
      $( [24-Oct-2004] $) $( [16-Nov-2000] $)

    $( Lemma for projection commutation theorem. $)
    pjclem2 $p |- ( G C_H H ->
       ( ( proj ` G ) o. ( proj ` H ) ) = ( ( proj ` H ) o. ( proj ` G ) ) ) $=
      ( ccm wbr cin cpj cfv ccom incom fveq2i pjclem1 wceq cmcmi sylbi 3eqtr4a
      ) ABEFZABGZHIBAGZHIZAHIZBHIZJUCUBJZSTHABKLABCDMRBAEFUDUANABCDOBADCMPQ $.
      $( [24-Oct-2004] $) $( [17-Nov-2000] $)

    $( Lemma for projection commutation theorem. $)
    pjclem3 $p |- ( ( ( proj ` G ) o. ( proj ` H ) ) =
                    ( ( proj ` H ) o. ( proj ` G ) ) ->
                     ( ( proj ` G ) o. ( proj ` ( _|_ ` H ) ) ) =
                     ( ( proj ` ( _|_ ` H ) ) o. ( proj ` G ) ) ) $=
      ( cpj cfv ccom wceq chil chod cort chio df-iop coeq2i pjfi hoid1i hoid1ri
      co eqtr3i coeq1i 3eqtr2i oveq1i oveq2 syl5eq helch pjddii 3eqtr4g 3eqtr3g
      hocsubdiri pjoci ) AEFZBEFZGZULUKGZHZUKIEFZULJRZGZUQUKGZUKBKFEFZGUTUKGUOU
      KUPGZUMJRZUPUKGZUNJRZURUSUOVBVCUMJRVDVAVCUMJVAUKLUKGVCUKLGVAUKLUPUKMNUKAC
      OZPSUKVEQLUPUKMTUAUBUMUNVCJUCUDUPULACIUEOZBDOZUFUPULUKVFVGVEUIUGUQUTUKBDU
      JZNUQUTUKVHTUH $.
      $( [26-Nov-2000] $)

    $( Lemma for projection commutation theorem. $)
    pjclem4a $p |- ( A e. ( G i^i H ) ->
               ( ( ( proj ` G ) o. ( proj ` H ) ) ` A ) = A ) $=
      ( cin wcel wa cpj cfv ccom wceq elin chil cheli adantl syl cch pjid mpan
      pjcoi wi eleq1 syl6bir eqeq2 sylibd impcom eqtrd sylbi ) ABCFGABGZACGZHZA
      BIJZCIJZKJZALABCMULUOAUNJZUMJZAULANGZUOUQLUKURUJACEOPABCDEUAQUKUJUQALZUKU
      PALZUJUSUBCRGUKUTEACSTUTUJUQUPLZUSUTUJUPBGZVAUPABUCBRGVBVADUPBSTUDUPAUQUE
      UFQUGUHUI $.
      $( [2-Dec-2000] $)

    $( Lemma for projection commutation theorem. $)
    pjclem4 $p |- ( ( ( proj ` G ) o. ( proj ` H ) ) =
                    ( ( proj ` H ) o. ( proj ` G ) ) ->
                     ( ( proj ` G ) o. ( proj ` H ) ) =
                     ( proj ` ( G i^i H ) ) ) $=
      ( vx vy cpj cfv wceq chil wcel wa co cva adantl csp cc0 cmin c0v pjfi cin
      ccom cv wral cmv cort pjcocli fveq1 eleq1d syl5ibr elin sylanbrc pjcohcli
      imp hvsubcl mpdan simpl adantr chincli cheli 3jca his2sub oveq1d pjadjcoi
      w3a sylan2 pjclem4a oveq2d eqtrd sylan9eq anim12i hicl subid 3syl 3eqtr2d
      syl cc expr ralrimiv csh wb chshii shocel pjvi syl2anc hvaddsub12 syl3anc
      ax-mp id hvsubid ax-hvaddid 3eqtrd fveq2d eqtr3d ralrimiva hocofi hoeqi
      sylib ) AGHZBGHZUBZWTWSUBZIZEUCZXAHZXDABUAZGHZHZIZEJUDXAXGIXCXIEJXCXDJKZL
      ZXEXDXEUEMZNMZXGHZXEXHXKXEXFKZXLXFUFHKZXNXEIXKXEAKZXEBKZXOXJXQXCXDABCDUGO
      XCXJXRXJXRXCXDXBHZBKXDBADCUGXCXEXSBXDXAXBUHZUIUJUNXEABUKULXKXLJKZXLFUCZPM
      ZQIZFXFUDZXPXJYAXCXJXEJKZYAXDABCDUMZXDXEUOUPOXKYDFXFXCXJYBXFKZYDXCXJYHLZL
      ZYCXDYBPMZXEYBPMZRMZYLYLRMZQYJXJYFYBJKZVEZYCYMIYIYPXCYIXJYFYOXJYHUQXJYFYH
      YGURYHYOXJYBXFABCDUSZUTZOVAOXDXEYBVBVPYJYLYKYLRXCYIYLXSYBPMZYKXCXEXSYBPXT
      VCYIYSXDYBXAHZPMZYKYHXJYOYSUUAIYRXDYBBADCVDVFYHUUAYKIXJYHYTYBXDPYBABCDVGV
      HOVIVJVCYJYFYOLZYLVQKYNQIYIUUBXCXJYFYHYOYGYRVKOXEYBVLYLVMVNVOVRVSXFVTKXPY
      AYELWAXFYQWBFXLXFWCWHULXEXLXFYQWDWEXJXNXHIXCXJXMXDXGXJXMXDXEXEUEMZNMZXDSN
      MXDXJYFXJYFXMUUDIYGXJWIYGXEXDXEWFWGXJUUCSXDNXJYFUUCSIYGXEWJVPVHXDWKWLWMOW
      NWOEXAXGWSWTACTBDTWPXFYQTWQWR $.
      $( [26-Nov-2000] $)

    $( Two subspaces commute iff their projections commute.  Lemma 4 of
       [Kalmbach] p. 67. $)
    pjci $p |- ( G C_H H <->
       ( ( proj ` G ) o. ( proj ` H ) ) = ( ( proj ` H ) o. ( proj ` G ) ) ) $=
      ( ccm cpj cfv ccom wceq cin cort co chos pjclem4 choccli coeq2i pjfi chio
      chj chincli wbr pjclem2 pjclem3 oveq12d pjtoi pjsdii df-iop hoid1i eqtr3i
      syl 3eqtr3ri wss inss2 chub2i sstri chdmm3i sseqtr4i pjscji ax-mp 3eqtr4g
      chil chjcli pj11i sylib cmbri sylibr impbii ) ABEUAZAFGZBFGZHZVJVIHIZABCD
      UBVLAABJZABKGZJZSLZIZVHVLVIVPFGZIVQVLVKVIVNFGZHZMLZVMFGZVOFGZMLZVIVRVLVKW
      BVTWCMABCDNVLVTVSVIHIVTWCIABCDUCAVNCBDOZNUJUDVIVJVSMLZHVIVAFGZHZWAVIWFWGV
      IBDUEPVJVSACBDQVNWEQUFVIRHWHVIRWGVIUGPVIACQUHUIUKVMVOKGZULVRWDIVMAKGZBSLZ
      WIVMBWKABUMBWJDACOUNUOABCDUPUQVMVOABCDTZAVNCWETZURUSUTAVPCVMVOWLWMVBVCVDA
      BCDVEVFVG $.
      $( [24-Oct-2004] $) $( [26-Nov-2000] $)

    $( A necessary and sufficient condition for the product of two projectors
       to be a projector is that the projectors commute.  Part 1 of Theorem 1
       of [AkhiezerGlazman] p. 65. $)
    pjcmul1i $p |- ( ( ( proj ` G ) o. ( proj ` H ) ) =
                   ( ( proj ` H ) o. ( proj ` G ) ) <->
                   ( ( proj ` G ) o. ( proj ` H ) ) e. ran proj ) $=
      ( cpj cfv ccom wceq crn wcel cin pjclem4 cch pjmfn chincli pjbdlni pjadj3
      wfn cado ax-mp fnfvelrn mp2an syl6eqel adjcoi coeq1i coeq2i 3eqtri pjadj2
      syl5reqr impbii ) AEFZBEFZGZULUKGZHZUMEIZJZUOUMABKZEFZUPABCDLEMRURMJUSUPJ
      NABCDOMUREUAUBUCUQUNUMSFZUMUTULSFZUKSFZGULVBGUNUKULACPBDPUDVAULVBBMJVAULH
      DBQTUEVBUKULAMJVBUKHCAQTUFUGUMUHUIUJ $.
      $( [3-Jun-2006] $)

    $( The projection subspace of the difference between two projectors.  Part
       2 of Theorem 1 of [AkhiezerGlazman] p. 65. $)
    pjcmul2i $p |- ( ( ( proj ` G ) o. ( proj ` H ) ) =
                   ( ( proj ` H ) o. ( proj ` G ) ) <->
                 ( ( proj ` G ) o. ( proj ` H ) ) = ( proj ` ( G i^i H ) ) ) $=
      ( cpj cfv ccom wceq cin pjclem4 crn wcel cch pjmfn chincli fnfvelrn mp2an
      wfn eleq1 mpbiri pjcmul1i sylibr impbii ) AEFZBEFZGZUEUDGHZUFABIZEFZHZABC
      DJUJUFEKZLZUGUJULUIUKLZEMRUHMLUMNABCDOMUHEPQUFUIUKSTABCDUAUBUC $.
      $( [3-Jun-2006] $)
  $}

  ${
    pjcohocl.1 $e |- H e. CH $.
    pjcohocl.2 $e |- T : ~H --> ~H $.
    $( Closure of composition of projection and Hilbert space operator. $)
    pjcohocli $p |- ( A e. ~H ->
         ( ( ( proj ` H ) o. T ) ` A ) e. H ) $=
      ( chil wcel cpj cfv ccom pjfi hocoi ffvelrni pjcli syl eqeltrd ) AFGZACHI
      ZBJIABIZRIZCARBCDKELQSFGTCGFFABEMSCDNOP $.
      $( [3-Dec-2000] $)
  $}

  ${
    $d x y F $.  $d x y G $.  $d x y H $.
    pjadj2co.1 $e |- F e. CH $.
    pjadj2co.2 $e |- G e. CH $.
    pjadj2co.3 $e |- H e. CH $.
    $( Adjoint of double composition of projections.  Generalization of special
       case of Theorem 3.11(viii) of [Beran] p. 106. $)
    pjadj2coi $p |- ( ( A e. ~H /\ B e. ~H ) ->
      ( ( ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) ` A ) .ih B ) =
    ( A .ih ( ( ( ( proj ` H ) o. ( proj ` G ) ) o. ( proj ` F ) ) ` B ) ) ) $=
      ( chil wcel wa cpj cfv ccom csp co wceq pjfi hocofi hocoi pjhcli pjadjcoi
      sylan pjadji sylan2 eqtrd oveq1d adantr coass fveq1i syl5eq oveq2d adantl
      pjcohcli 3eqtr4d ) AIJZBIJZKZAELMZMZCLMZDLMZNZMZBOPZABVBVANZMZUSMZOPZAVCU
      SNMZBOPZABUSVBNVANZMZOPZURVEUTVGOPZVIUPUTIJUQVEVOQAEHUAUTBCDFGUBUCUQUPVGI
      JVOVIQBDCGFUNAVGEHUDUEUFUPVKVEQUQUPVJVDBOAVCUSVAVBCFRZDGRZSEHRZTUGUHUQVNV
      IQUPUQVMVHAOUQVMBUSVFNZMVHBVLVSUSVBVAUIUJBUSVFVRVBVAVQVPSTUKULUMUO $.
      $( [1-Dec-2000] $)

    $( Closure of double composition of projections. $)
    pj2cocli $p |- ( A e. ~H ->
         ( ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) ` A ) e. F ) $=
      ( chil wcel cpj cfv ccom pjfi ho2coi pjhcli pjcli 3syl eqeltrd ) AHIZABJK
      ZCJKZLDJKZLKAUBKZUAKZTKZBATUAUBBEMCFMDGMNSUCHIUDHIUEBIADGOUCCFOUDBEPQR $.
      $( [2-Dec-2000] $)

    $( Lemma for projection triplet theorem. $)
    pj3lem1 $p |- ( A e. ( ( F i^i G ) i^i H ) ->
         ( ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) ` A ) = A ) $=
      ( cin wcel cpj cfv ccom wceq wa elin chil cheli adantr pjfi syl hocofi wi
      hocoi pjclem4a eleq1 cch pjid mpan syl6bir eqeq2 sylibd eqtrd sylbi inass
      impcom eleq2i coass fveq1i eqeq1i 3imtr4i ) ABCDHZHZIZABJKZCJKZDJKZLZLZKZ
      AMZABCHDHZIAVDVELVFLZKZAMVCABIZAVAIZNZVJABVAOVPVIAVGKZVDKZAVPAPIZVIVRMVNV
      SVOABEQRAVDVGBESVEVFCFSDGSUAUCTVOVNVRAMZVOVQAMZVNVTUBACDFGUDWAVNVRVQMZVTW
      AVNVQBIZWBVQABUEBUFIWCWBEVQBUGUHUIVQAVRUJUKTUOULUMVKVBABCDUNUPVMVIAAVLVHV
      DVEVFUQURUSUT $.
      $( [2-Dec-2000] $)

    $( Stronger projection triplet theorem. $)
    pj3si $p |- ( ( ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) =
                  ( ( ( proj ` H ) o. ( proj ` G ) ) o. ( proj ` F ) ) /\
         ran ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) C_ G ) ->
       ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) =
       ( proj ` ( ( F i^i G ) i^i H ) ) ) $=
      ( vx vy cpj cfv ccom wceq wa chil wcel co cva adantl pjfi csp crn wss cin
      cv wral cmv cort pj2cocli wfn hocofi hocofni fnfvelrn mpan ssel syl5 elin
      imp sylanbrc adantll fveq1 syl5ibr adantlr cc0 hococli hvsubcl mpdan cmin
      eleq1d w3a simpl adantr chincli cheli his2sub syl oveq1d pjadj2coi sylan2
      3jca pj3lem1 oveq2d eqtrd sylan9eq cc anim12i hicl subid 3eqtr2d ralrimiv
      3syl expr csh chshii shocel ax-mp pjvi syl2anc hvaddsub12 syl3anc hvsubid
      wb c0v id ax-hvaddid 3eqtrd fveq2d eqtr3d ralrimiva hoeqi sylib ) AIJZBIJ
      ZKZCIJZKZXNXLKXKKZLZXOUAZBUBZMZGUDZXOJZYAABUCZCUCZIJZJZLZGNUEXOYELXTYGGNX
      TYANOZMZYBYAYBUFPZQPZYEJZYBYFYIYBYDOZYJYDUGJOZYLYBLYIYBYCOZYBCOZYMXSYHYOX
      QXSYHMYBAOZYBBOZYOYHYQXSYAABCDEFUHRXSYHYRYHYBXROZXSYRXONUIYHYSXMXNXKXLADS
      BESUJZCFSZUKNYAXOULUMXRBYBUNUOUQYBABUPURUSXQYHYPXSXQYHYPYHYPXQYAXPJZCOYAC
      BAFEDUHXQYBUUBCYAXOXPUTZVHVAUQVBYBYCCUPURYIYJNOZYJHUDZTPZVCLZHYDUEZYNYHUU
      DXTYHYBNOZUUDYAXMXNYTUUAVDZYAYBVEVFRYIUUGHYDXTYHUUEYDOZUUGXTYHUUKMZMZUUFY
      AUUETPZYBUUETPZVGPZUUOUUOVGPZVCUUMYHUUIUUENOZVIZUUFUUPLUULUUSXTUULYHUUIUU
      RYHUUKVJYHUUIUUKUUJVKUUKUURYHUUEYDYCCABDEVLFVLZVMZRVSRYAYBUUEVNVOUUMUUOUU
      NUUOVGXTUULUUOUUBUUETPZUUNXTYBUUBUUETXQYBUUBLXSUUCVKVPUULUVBYAUUEXOJZTPZU
      UNUUKYHUURUVBUVDLUVAYAUUECBAFEDVQVRUUKUVDUUNLYHUUKUVCUUEYATUUEABCDEFVTWAR
      WBWCVPUUMUUIUURMZUUOWDOUUQVCLUULUVEXTYHUUIUUKUURUUJUVAWERYBUUEWFUUOWGWJWH
      WKWIYDWLOYNUUDUUHMXAYDUUTWMHYJYDWNWOURYBYJYDUUTWPWQYHYLYFLXTYHYKYAYEYHYKY
      AYBYBUFPZQPZYAXBQPYAYHUUIYHUUIYKUVGLUUJYHXCUUJYBYAYBWRWSYHUVFXBYAQYHUUIUV
      FXBLUUJYBWTVOWAYAXDXEXFRXGXHGXOYEXMXNYTUUAUJYDUUTSXIXJ $.
      $( [2-Dec-2000] $)

    $( Projection triplet theorem. $)
    pj3i $p |- ( ( ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) =
                  ( ( ( proj ` H ) o. ( proj ` G ) ) o. ( proj ` F ) ) /\
                  ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) =
                  ( ( ( proj ` G ) o. ( proj ` F ) ) o. ( proj ` H ) ) ) ->
       ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) =
       ( proj ` ( ( F i^i G ) i^i H ) ) ) $=
      ( cpj cfv ccom wceq crn wss rncoss pjrni sseqtri coass eqeq1 mpbiri rneqd
      cin sseq1d pj3si sylan2 ) AGHZBGHZICGHZIZUEUDIUFIZJZUGUFUEIUDIJUGKZBLZUGA
      BTCTGHJUIUKUEUDUFIZIZKZBLUNUEKBUEULMBENOUIUJUNBUIUGUMUIUGUMJUHUMJUEUDUFPU
      GUHUMQRSUARABCDEFUBUC $.
      $( [2-Dec-2000] $)

    $( Projection triplet corollary. $)
    pj3cor1i $p |- ( ( ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) =
                  ( ( ( proj ` H ) o. ( proj ` G ) ) o. ( proj ` F ) ) /\
                  ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) =
                  ( ( ( proj ` G ) o. ( proj ` F ) ) o. ( proj ` H ) ) ) ->
       ( ( ( proj ` F ) o. ( proj ` G ) ) o. ( proj ` H ) ) =
       ( ( ( proj ` H ) o. ( proj ` F ) ) o. ( proj ` G ) ) ) $=
      ( vx vy cpj cfv ccom wceq wa chil wcel csp co ad2antlr pjfi hocofi oveq2d
      wral fveq1 adantl cin chincli pjadji adantlr pj3i fveq1d oveq1d pjadj2coi
      cv 3eqtr4d exp31 ralrimdv wb hococli hial2eq syl2anc com12 ralrimiv hoeqi
      sylibd sylib ) AIJZBIJZKZCIJZKZVIVGKVFKLZVJVGVFKVIKZLZMZGUMZVJJZVOVIVFKZV
      GKZJZLZGNUBVJVRLVNVTGNVONOZVNVTWAVNVPHUMZPQZVSWBPQZLZHNUBZVTWAVNWEHNWAVNW
      BNOZWEWAVNMWGMZVOWBVJJZPQZVOWBVLJZPQZWCWDVNWJWLLZWAWGVMWMVKVMWIWKVOPWBVJV
      LUCUAUDRWHVOABUEZCUEZIJZJZWBPQZVOWBWPJZPQZWCWJWAWGWRWTLVNVOWBWOWNCABDEUFF
      UFUGUHVNWCWRLWAWGVNVPWQWBPVNVOVJWPABCDEFUIZUJUKRVNWJWTLWAWGVNWIWSVOPVNWBV
      JWPXAUJUARUNWAWGWDWLLVNVOWBCABFDEULUHUNUOUPWAVPNOVSNOWFVTUQVOVHVIVFVGADSZ
      BESZTZCFSZURVOVQVGVIVFXEXBTZXCURHVPVSUSUTVDVAVBGVJVRVHVIXDXETVQVGXFXCTVCV
      E $.
      $( [2-Dec-2000] $)
  $}

  ${
    pjs14.1 $e |- G e. CH $.
    pjs14.2 $e |- H e. CH $.
    $( Theorem S-14 of Watanabe, p. 486. $)
    pjs14i $p |- ( A e. ~H ->
                ( normh ` ( ( ( proj ` H ) o. ( proj ` G ) ) ` A ) )
                <_ ( normh ` ( ( proj ` G ) ` A ) ) ) $=
      ( chil wcel cpj cfv ccom cno cle fveq2d cch pjhcli pjnorm sylancr eqbrtrd
      pjcoi wbr ) AFGZACHIZBHIZJIZKIAUCIZUBIZKIZUEKIZLUAUDUFKACBEDSMUACNGUEFGUG
      UHLTEABDOUECPQR $.
      $( [26-Sep-2001] $)
  $}

