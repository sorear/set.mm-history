$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Hilbert lattice operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The zero subspace is the smallest subspace. $)
  sh0le $p |- ( A e. SH -> 0H C_ A ) $=
    ( csh wcel c0h c0v csn df-ch0 sh0 snssd syl5eqss ) ABCZDEFAGKEAAHIJ $.
    $( [3-Jun-2004] $)

  $( The zero subspace is the smallest member of ` CH ` . $)
  ch0le $p |- ( A e. CH -> 0H C_ A ) $=
    ( cch wcel csh c0h wss chsh sh0le syl ) ABCADCEAFAGAHI $.
    $( [14-Aug-2002] $)

  $( No subspace is smaller than the zero subspace. $)
  shle0 $p |- ( A e. SH -> ( A C_ 0H <-> A = 0H ) ) $=
    ( csh wcel c0h wss wa wceq sh0le biantrud eqss syl6bbr ) ABCZADEZMDAEZFADGL
    NMAHIADJK $.
    $( [24-Nov-2004] $)

  $( No Hilbert lattice element is smaller than zero. $)
  chle0 $p |- ( A e. CH -> ( A C_ 0H <-> A = 0H ) ) $=
    ( cch wcel csh c0h wss wceq wb chsh shle0 syl ) ABCADCAEFAEGHAIAJK $.
    $( [14-Aug-2002] $)

  $( A Hilbert lattice element that is not a subset of another is nonzero. $)
  chnlen0 $p |- ( B e. CH -> ( -. A C_ B -> -. A = 0H ) ) $=
    ( cch wcel c0h wceq wss ch0le sseq1 syl5ibrcom con3d ) BCDZAEFZABGZLNMEBGBH
    AEBIJK $.
    $( [30-Jun-2004] $)

  $( The zero subspace is a proper subset of nonzero Hilbert lattice
     elements. $)
  ch0pss $p |- ( A e. CH -> ( 0H C. A <-> A =/= 0H ) ) $=
    ( cch wcel c0h wne wss wpss necom ch0le biantrurd syl5bbr df-pss syl6rbbr
    wa ) ABCZADEZDAFZDAEZNZDAGPROSDAHOQRAIJKDALM $.
    $( [18-Jun-2006] $) $( [9-Jun-2004] $)

  $( The intersection of orthogonal subspaces is the zero subspace. $)
  orthin $p |- ( ( A e. SH /\ B e. SH ) ->
               ( A C_ ( _|_ ` B ) -> ( A i^i B ) = 0H ) ) $=
    ( csh wcel wa cort cfv wss cin c0h wceq wi ssrin incom syl6sseq ocin sseq2d
    syl5ib adantl shincl sh0le syl jctird eqss syl6ibr ) ACDZBCDZEZABFGZHZABIZJ
    HZJUKHZEUKJKUHUJULUMUGUJULLUFUJUKBUIIZHUGULUJUKUIBIUNAUIBMUIBNOUGUNJUKBPQRS
    UHUKCDUMABTUKUAUBUCUKJUDUE $.
    $( [24-Jun-2004] $)
  $( The lattice join of a subset with its orthocomplement is the whole space.
     (Contributed by Mario Carneiro, 15-May-2014.) $)
  ssjo $p |- ( A C_ ~H -> ( A vH ( _|_ ` A ) ) = ~H ) $=
    ( chil wss cort cfv chj co cun wceq mpdan c0h wa wi sylib mpi csh wcel ocsh
    occon syl ocss sshjval cin ssun1 ancli unss ssun2 syl2anc ssin ocin sseqtrd
    jca sh0le eqssd fveq2d choc0 syl6eq eqtrd ) ABCZAADEZFGZAUTHZDEZDEZBUSUTBCZ
    VAVDIAUAZAUTUBJUSVDKDEBUSVCKDUSVCKUSVCUTUTDEZUCZKUSVCUTCZVCVGCZLVCVHCUSVIVJ
    USAVBCZVIAUTUDUSVBBCZVKVIMUSUSVELVLUSVEVFUEAUTBUFNZAVBSJOUSUTVBCZVJUTAUGUSV
    EVLVNVJMVFVMUTVBSUHOULVCUTVGUINUSUTPQVHKIARUTUJTUKUSVCPQZKVCCUSVLVOVMVBRTVC
    UMTUNUOUPUQUR $.
    $( [15-May-2014] $)

  ${
    $d x y A $.
    shne0.1 $e |- A e. SH $.
    $( A nonzero subspace has a nonzero vector. $)
    shne0i $p |- ( A =/= 0H <-> E. x e. A x =/= 0h ) $=
      ( c0h wne wceq wn cv wcel wrex c0v df-ne wa wex wss df-rex nss csh shle0
      wb ax-mp notbii 3bitr2ri elch0 necon3bbii rexbii 3bitri ) BDEBDFZGZAHZDIZ
      GZABJZUJKEZABJBDLUMUJBIULMANBDOZGUIULABPABDQUOUHBRIUOUHTCBSUAUBUCULUNABUK
      UJKUJUDUEUFUG $.
      $( [25-Feb-2006] $)

    $( Hilbert subspace sum with the zero subspace. $)
    shs0i $p |- ( A +H 0H ) = A $=
      ( c0h cph co cun cspn cfv h0elsh shsval3i wss wceq csh wcel sh0le ssequn2
      ax-mp mpbi fveq2i spanid 3eqtri ) ACDEACFZGHAGHZAACBIJUBAGCAKZUBALAMNZUDB
      AOQCAPRSUEUCALBATQUA $.
      $( [14-Jan-2005] $)

    ${
      shs00.2 $e |- B e. SH $.
      $( Two subspaces are zero iff their join is zero. $)
      shs00i $p |- ( ( A = 0H /\ B = 0H ) <-> ( A +H B ) = 0H ) $=
        ( c0h wceq wa cph co oveq12 h0elsh wss sseq2 mpbii csh wcel shle0 ax-mp
        wb sylib shs0i syl6eq shsub1i shsub2i jca impbii ) AEFZBEFZGZABHIZEFZUI
        UJEEHIEAEBEHJEKUAUBUKUGUHUKAELZUGUKAUJLULABCDUCUJEAMNAOPULUGSCAQRTUKBEL
        ZUHUKBUJLUMBADCUDUJEBMNBOPUMUHSDBQRTUEUF $.
        $( [7-Aug-2004] $)
    $}
  $}

  ${
    $d x y A $.
    ch0le.1 $e |- A e. CH $.
    $( The closed subspace zero is the smallest member of ` CH ` . $)
    ch0lei $p |- 0H C_ A $=
      ( cch wcel c0h wss ch0le ax-mp ) ACDEAFBAGH $.
      $( [15-Oct-1999] $)

    $( No Hilbert closed subspace is smaller than zero. $)
    chle0i $p |- ( A C_ 0H <-> A = 0H ) $=
      ( cch wcel c0h wss wceq wb chle0 ax-mp ) ACDAEFAEGHBAIJ $.
      $( [7-Apr-2001] $)

    $( A nonzero closed subspace has a nonzero vector. $)
    chne0i $p |- ( A =/= 0H <-> E. x e. A x =/= 0h ) $=
      ( chshii shne0i ) ABBCDE $.
      $( [25-Feb-2006] $)

    $( Intersection of a closed subspace and its orthocomplement.  Part of
       Proposition 1 of [Kalmbach] p. 65. $)
    chocini $p |- ( A i^i ( _|_ ` A ) ) = 0H $=
      ( csh wcel cort cfv cin c0h wceq chshii ocin ax-mp ) ACDAAEFGHIABJAKL $.
      $( [6-Aug-2004] $) $( [11-Oct-1999] $)

    $( Join with lattice zero in ` CH ` . $)
    chj0i $p |- ( A vH 0H ) = A $=
      ( c0h chj cun cort cfv h0elch chjvali wss wceq ch0lei ssequn2 mpbi fveq2i
      co pjococi 3eqtri ) ACDPACEZFGZFGAFGZFGAACBHITUAFSAFCAJSAKABLCAMNOOABQR
      $.
      $( [15-Oct-1999] $)

    $( Meet with lattice one in ` CH ` . $)
    chm1i $p |- ( A i^i ~H ) = A $=
      ( chil wss cin wceq chssii df-ss mpbi ) ACDACEAFABGACHI $.
      $( [24-Oct-1999] $)

    ${
      $d x y B $.  $d x y C $.
      chjcl.2 $e |- B e. CH $.
      $( Closure of ` CH ` join. $)
      chjcli $p |- ( A vH B ) e. CH $=
        ( chshii shjcli ) ABACEBDEF $.
        $( [29-Jul-1999] $)

      $( Subspace sum is smaller than subspace join.  Remark in [Kalmbach]
         p. 65. $)
      chsleji $p |- ( A +H B ) C_ ( A vH B ) $=
        ( chshii shsleji ) ABACEBDEF $.
        $( [17-Oct-1999] $)

      $( Membership in subspace sum. $)
      chseli $p |- ( C e. ( A +H B )
           <-> E. x e. A E. y e. B C = ( x +h y ) ) $=
        ( chshii shseli ) ABCDECFHDGHI $.
        $( [19-Oct-1999] $)

      $( Closure of Hilbert lattice intersection. $)
      chincli $p |- ( A i^i B ) e. CH $=
        ( cpr cint cin cch elexi intpr wss c0 wne wcel wa pm3.2i prss mpbi prnz
        chintcli eqeltrri ) ABEZFABGHABAHCIZBHDIZJUBUBHKZUBLMAHNZBHNZOUEUFUGCDP
        ABHUCUDQRABUCSPTUA $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chsscon3i $p |- ( A C_ B <-> ( _|_ ` B ) C_ ( _|_ ` A ) ) $=
        ( wss cort cfv chil chssii occon mp2an choccli pjococi 3sstr3g impbii
        wi ) ABEZBFGZAFGZEZAHEBHEQTPACIBDIABJKTSFGZRFGZABRHESHETUAUBEPRBDLISACL
        IRSJKACMBDMNO $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chsscon1i $p |- ( ( _|_ ` A ) C_ B <-> ( _|_ ` B ) C_ A ) $=
        ( cort cfv wss choccli chsscon3i pjococi sseq2i bitri ) AEFZBGBEFZMEFZG
        NAGMBACHDIOANACJKL $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chsscon2i $p |- ( A C_ ( _|_ ` B ) <-> B C_ ( _|_ ` A ) ) $=
        ( chil wss cort cfv wb chssii occon3 mp2an ) AEFBEFABGHFBAGHFIACJBDJABK
        L $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chcon2i $p |- ( A = ( _|_ ` B ) <-> B = ( _|_ ` A ) ) $=
        ( cort cfv wss wa wceq chsscon2i chsscon1i anbi12i eqss 3bitr4i ) ABEFZ
        GZOAGZHBAEFZGZRBGZHAOIBRIPSQTABCDJBADCKLAOMBRMN $.
        $( [24-Jun-2004] $)

      $( Hilbert lattice contraposition law. $)
      chcon1i $p |- ( ( _|_ ` A ) = B <-> ( _|_ ` B ) = A ) $=
        ( cort cfv wceq chcon2i eqcom 3bitr4i ) BAEFZGABEFZGKBGLAGBADCHKBILAIJ
        $.
        $( [15-Jun-2006] $)

      $( Hilbert lattice contraposition law. $)
      chcon3i $p |- ( A = B <-> ( _|_ ` B ) = ( _|_ ` A ) ) $=
        ( wss wa cort cfv wceq chsscon3i anbi12i eqss 3bitr4i ) ABEZBAEZFBGHZAG
        HZEZQPEZFABIPQINROSABCDJBADCJKABLPQLM $.
        $( [24-Jun-2004] $)

      $( Union is smaller than ` CH ` join. $)
      chunssji $p |- ( A u. B ) C_ ( A vH B ) $=
        ( chshii shunssji ) ABACEBDEF $.
        $( [15-Oct-1999] $)

      $( Commutative law for join in ` CH ` . $)
      chjcomi $p |- ( A vH B ) = ( B vH A ) $=
        ( chshii shjcomi ) ABACEBDEF $.
        $( [14-Oct-1999] $)

      $( ` CH ` join is an upper bound of two elements. $)
      chub1i $p |- A C_ ( A vH B ) $=
        ( chshii shub1i ) ABACEBDEF $.
        $( [15-Oct-1999] $)

      $( ` CH ` join is an upper bound of two elements. $)
      chub2i $p |- A C_ ( B vH A ) $=
        ( chj co chub1i chjcomi sseqtri ) AABEFBAEFABCDGABCDHI $.
        $( [5-Nov-2000] $)

      ${
        $d x y C $.
        chlub.1 $e |- C e. CH $.
        $( Hilbert lattice join is the least upper bound of two elements. $)
        chlubi $p |- ( ( A C_ C /\ B C_ C ) <-> ( A vH B ) C_ C ) $=
          ( chshii shlubi ) ABCADGBEGFH $.
          $( [11-Jun-2004] $)

        $( Hilbert lattice join is the least upper bound of two elements (one
           direction of ~ chlubi ). $)
        chlubii $p |- ( ( A C_ C /\ B C_ C ) -> ( A vH B ) C_ C ) $=
          ( wss wa chj co chlubi biimpi ) ACGBCGHABIJCGABCDEFKL $.
          $( [15-Oct-1999] $)

        $( Add join to both sides of a Hilbert lattice ordering. $)
        chlej1i $p |- ( A C_ B -> ( A vH C ) C_ ( B vH C ) ) $=
          ( chshii shlej1i ) ABCADGBEGCFGH $.
          $( [19-Oct-1999] $)

        $( Add join to both sides of a Hilbert lattice ordering. $)
        chlej2i $p |- ( A C_ B -> ( C vH A ) C_ ( C vH B ) ) $=
          ( chshii shlej2i ) ABCADGBEGCFGH $.
          $( [19-Oct-1999] $)

        ${
          chlej12.4 $e |- D e. CH $.
          $( Add join to both sides of a Hilbert lattice ordering. $)
          chlej12i $p |- ( ( A C_ B /\ C C_ D ) ->
                       ( A vH C ) C_ ( B vH D ) ) $=
            ( wss chj co chlej1i chlej2i sylan9ss ) ABICDIACJKBCJKBDJKABCEFGLCD
            BGHFMN $.
            $( [19-Oct-1999] $)
        $}
      $}

      $( Hilbert lattice ordering in terms of join. $)
      chlejb1i $p |- ( A C_ B <-> ( A vH B ) = B ) $=
        ( wss chj co wceq wa ssid chlubii mpan2 chub2i jctir eqss sylibr chub1i
        eqimss sstr2 mpsyl impbii ) ABEZABFGZBHZUBUCBEZBUCEZIUDUBUEUFUBBBEUEBJA
        BBCDDKLBADCMNUCBOPAUCEUDUEUBABCDQUCBRAUCBSTUA $.
        $( [15-Oct-1999] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm1i $p |- ( _|_ ` ( A i^i B ) ) = ( ( _|_ ` A ) vH ( _|_ ` B ) ) $=
        ( cin cort cfv chj co choccli chub1i chjcli chsscon1i mpbi chub2i ssini
        wss chincli inss1 chsscon3i inss2 chlubii mp2an eqssi ) ABEZFGZAFGZBFGZ
        HIZUIFGZUEQUFUIQUJABUGUIQUJAQUGUHACJZBDJZKAUICUGUHUKULLZMNUHUIQUJBQUHUG
        ULUKOBUIDUMMNPUIUEUMABCDRZMNUGUFQZUHUFQZUIUFQUEAQUOABSUEAUNCTNUEBQUPABU
        AUEBUNDTNUGUHUFUKULUEUNJUBUCUD $.
        $( [21-Jun-2004] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm2i $p |- ( _|_ ` ( ( _|_ ` A ) i^i B ) ) = ( A vH ( _|_ ` B ) ) $=
        ( cort cfv cin chj co choccli chdmm1i pjococi oveq1i eqtri ) AEFZBGEFOE
        FZBEFZHIAQHIOBACJDKPAQHACLMN $.
        $( [21-Jun-2004] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm3i $p |- ( _|_ ` ( A i^i ( _|_ ` B ) ) ) = ( ( _|_ ` A ) vH B ) $=
        ( cort cfv cin chj co choccli chdmm1i pjococi oveq2i eqtri ) ABEFZGEFAE
        FZOEFZHIPBHIAOCBDJKQBPHBDLMN $.
        $( [21-Jun-2004] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm4i $p |- ( _|_ ` ( ( _|_ ` A ) i^i ( _|_ ` B ) ) ) = ( A vH B ) $=
        ( cort cfv cin chj co choccli chdmm2i pjococi oveq2i eqtri ) AEFBEFZGEF
        AOEFZHIABHIAOCBDJKPBAHBDLMN $.
        $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj1i $p |- ( _|_ ` ( A vH B ) ) = ( ( _|_ ` A ) i^i ( _|_ ` B ) ) $=
        ( cort cfv cin chj co chdmm4i fveq2i choccli chincli pjococi eqtr3i ) A
        EFZBEFZGZEFZEFABHIZEFRSTEABCDJKRPQACLBDLMNO $.
        $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj2i $p |- ( _|_ ` ( ( _|_ ` A ) vH B ) ) = ( A i^i ( _|_ ` B ) ) $=
        ( cort cfv chj co cin choccli chdmj1i pjococi ineq1i eqtri ) AEFZBGHEFO
        EFZBEFZIAQIOBACJDKPAQACLMN $.
        $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj3i $p |- ( _|_ ` ( A vH ( _|_ ` B ) ) ) = ( ( _|_ ` A ) i^i B ) $=
        ( cort cfv chj co cin choccli chdmj1i pjococi ineq2i eqtri ) ABEFZGHEFA
        EFZOEFZIPBIAOCBDJKQBPBDLMN $.
        $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj4i $p |- ( _|_ ` ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) = ( A i^i B ) $=
        ( cort cfv chj co cin choccli chdmj2i pjococi ineq2i eqtri ) AEFBEFZGHE
        FAOEFZIABIAOCBDJKPBABDLMN $.
        $( [21-Jun-2004] $)

      $( Equivalent expressions for "not less than" in the Hilbert lattice. $)
      chnlei $p |- ( -. B C_ A <-> A C. ( A vH B ) ) $=
        ( chj co wceq wn wss wpss chub1i biantrur chlejb1i eqcom chjcomi eqeq2i
        wa 3bitri notbii dfpss2 3bitr4i ) AABEFZGZHZAUBIZUDQBAIZHAUBJUEUDABCDKL
        UFUCUFBAEFZAGAUGGUCBADCMUGANUGUBABADCOPRSAUBTUA $.
        $( [5-Jun-2004] $)

      ${
        chjass.3 $e |- C e. CH $.
        $( Associative law for Hilbert lattice join.  From definition of
           lattice in [Kalmbach] p. 14. $)
        chjassi $p |- ( ( A vH B ) vH C ) = ( A vH ( B vH C ) ) $=
          ( chj cort cfv cin inass chdmj1i ineq1i ineq2i 3eqtr4i fveq2i chdmm4i
          co chjcli 3eqtr3i ) ABGRZHIZCHIZJZHIAHIZBCGRZHIZJZHIUACGRAUFGRUDUHHUE
          BHIZJZUCJUEUIUCJZJUDUHUEUIUCKUBUJUCABDELMUGUKUEBCEFLNOPUACABDESFQAUFD
          BCEFSQT $.
          $( [10-Jun-2004] $)
      $}

      $( Two Hilbert lattice elements are zero iff their join is zero. $)
      chj00i $p |- ( ( A = 0H /\ B = 0H ) <-> ( A vH B ) = 0H ) $=
        ( c0h wceq wa chj co oveq12 h0elch chj0i syl6eq wss chub1i sseq2 chle0i
        mpbii sylib chub2i jca impbii ) AEFZBEFZGZABHIZEFZUEUFEEHIEAEBEHJEKLMUG
        UCUDUGAENZUCUGAUFNUHABCDOUFEAPRACQSUGBENZUDUGBUFNUIBADCTUFEBPRBDQSUAUB
        $.
        $( [7-Aug-2004] $)
    $}

    $( The join of a closed subspace and its orthocomplement. $)
    chjoi $p |- ( A vH ( _|_ ` A ) ) = ~H $=
      ( chil wss cort cfv chj co wceq chssii ssjo ax-mp ) ACDAAEFGHCIABJAKL $.
      $( [24-Oct-1999] $)

    $( Join with Hilbert lattice unit. $)
    chj1i $p |- ( A vH ~H ) = ~H $=
      ( chil chj co helch chjcli chssii chub2i eqssi ) ACDEZCKACBFGHCAFBIJ $.
      $( [6-Aug-2004] $)

    $( Meet with Hilbert lattice zero. $)
    chm0i $p |- ( A i^i 0H ) = 0H $=
      ( c0h cin inss2 ch0lei ssid ssini eqssi ) ACDCACECACABFCGHI $.
      $( [6-Aug-2004] $)

  $}

  $( Meet with Hilbert lattice zero. $)
  chm0 $p |- ( A e. CH -> ( A i^i 0H ) = 0H ) $=
    ( cch wcel c0h cin wceq cif ineq1 eqeq1d h0elch elimel chm0i dedth ) ABCZAD
    EZDFNADGZDEZDFADAPFOQDAPDHIPADBJKLM $.
    $( [14-Jun-2006] $)

  ${
    shjshs.1 $e |- A e. SH $.
    shjshs.2 $e |- B e. SH $.
    $( Hilbert lattice join equals the double orthocomplement of subspace
       sum. $)
    shjshsi $p |- ( A vH B ) = ( _|_ ` ( _|_ ` ( A +H B ) ) ) $=
      ( chj cph cort cfv cun csh wcel wceq shjval mp2an wss shunssi chil shssii
      co ax-mp unssi shscli occon2i eqsstri shsleji wi shjcli chssii occon occl
      cch chsscon1i mpbi eqssi ) ABESZABFSZGHZGHZUOABIZGHGHZURAJKBJKUOUTLCDABMN
      USUPOUTUROABCDPUSUPABQACRBDRUAUPABCDUBRZUCTUDUOGHUQOZURUOOUPUOOZVBABCDUEU
      PQOZUOQOVCVBUFVAUOABCDUGZUHUPUOUINTUOUQVEVDUQUKKVAUPUJTULUMUN $.
      $( [15-May-2014] $) $( [27-Nov-2004] $)

    $( A closed subspace sum equals Hilbert lattice join.  Part of Lemma 31.1.5
       of [MaedaMaeda] p. 136. $)
    shjshseli $p |- ( ( A +H B ) e. CH <-> ( A +H B ) = ( A vH B ) ) $=
      ( cph cch wcel chj wceq cort cfv shjshsi ococ syl5req shjcli eleq1 mpbiri
      co impbii ) ABERZFGZTABHRZIZUAUBTJKJKTABCDLTMNUCUAUBFGABCDOTUBFPQS $.
      $( [30-Nov-2004] $)
  $}

  ${
    $d x A $.
    $( A nonzero closed subspace has a nonzero vector. $)
    chne0 $p |- ( A e. CH -> ( A =/= 0H <-> E. x e. A x =/= 0h ) ) $=
      ( cch wcel c0h wne cv c0v wrex cif wceq neeq1 rexeq bibi12d h0elch elimel
      wb chne0i dedth ) BCDZBEFZAGHFZABIZQTBEJZEFZUBAUDIZQBEBUDKUAUEUCUFBUDELUB
      ABUDMNAUDBECOPRS $.
      $( [25-Feb-2006] $)
  $}

  $( Intersection of a closed subspace and its orthocomplement.  Part of
     Proposition 1 of [Kalmbach] p. 65. $)
  chocin $p |- ( A e. CH -> ( A i^i ( _|_ ` A ) ) = 0H ) $=
    ( cch wcel cort cfv cin c0h wceq cif id fveq2 ineq12d eqeq1d h0elch chocini
    elimel dedth ) ABCZAADEZFZGHRAGIZUADEZFZGHAGAUAHZTUCGUDAUASUBUDJAUADKLMUAAG
    BNPOQ $.
    $( [13-Jun-2006] $)

  $( A closed subspace less than its orthocomplement is zero. $)
  chssoc $p |- ( A e. CH -> ( A C_ ( _|_ ` A ) <-> A = 0H ) ) $=
    ( cch wcel cort cfv wss c0h inidm sslin syl5eqssr chocin sseq2d chle0 bitrd
    wceq cin syl5ib wa simpr choccl ch0le syl adantr eqsstrd ex impbid ) ABCZAA
    DEZFZAGOZUIAAUHPZFZUGUJUIAAAPUKAHAUHAIJUGULAGFUJUGUKGAAKLAMNQUGUJUIUGUJRAGU
    HUGUJSUGGUHFZUJUGUHBCUMATUHUAUBUCUDUEUF $.
    $( [14-Jun-2006] $)

  $( Join with Hilbert lattice zero. $)
  chj0 $p |- ( A e. CH -> ( A vH 0H ) = A ) $=
    ( cch wcel c0h chj co wceq cif oveq1 id eqeq12d h0elch elimel chj0i dedth )
    ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [22-Jun-2004] $)

  $( Subspace sum is smaller than subspace join.  Remark in [Kalmbach]
     p. 65. $)
  chslej $p |- ( ( A e. CH /\ B e. CH ) -> ( A +H B ) C_ ( A vH B ) ) $=
    ( cch wcel csh cph co chj wss chsh shslej syl2an ) ACDAEDBEDABFGABHGIBCDAJB
    JABKL $.
    $( [12-Jul-2004] $)

  $( Closure of Hilbert lattice intersection. $)
  chincl $p |- ( ( A e. CH /\ B e. CH ) -> ( A i^i B ) e. CH ) $=
    ( cch wcel cin chil wceq ineq1 eleq1d ineq2 helch elimel chincli dedth2h
    cif ) ACDZBCDZABEZCDPAFOZBEZCDSQBFOZEZCDABFFASGRTCASBHIBUAGTUBCBUASJISUAAFC
    KLBFCKLMN $.
    $( [15-Jun-2004] $)

  $( Hilbert lattice contraposition law. $)
  chsscon3 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A C_ B <-> ( _|_ ` B ) C_ ( _|_ ` A ) ) ) $=
    ( cch wcel wss cort cfv wb chil cif sseq1 fveq2 sseq2d bibi12d sseq2 sseq1d
    wceq helch elimel chsscon3i dedth2h ) ACDZBCDZABEZBFGZAFGZEZHUBAIJZBEZUEUHF
    GZEZHUHUCBIJZEZULFGZUJEZHABIIAUHQZUDUIUGUKAUHBKUPUFUJUEAUHFLMNBULQZUIUMUKUO
    BULUHOUQUEUNUJBULFLPNUHULAICRSBICRSTUA $.
    $( [12-Jun-2004] $)

  $( Hilbert lattice contraposition law. $)
  chsscon1 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( ( _|_ ` A ) C_ B <-> ( _|_ ` B ) C_ A ) ) $=
    ( cch wcel wa cort cfv wss wb choccl chsscon3 sylan wceq ococ adantr sseq2d
    bitrd ) ACDZBCDZEZAFGZBHZBFGZUAFGZHZUCAHRUACDSUBUEIAJUABKLTUDAUCRUDAMSANOPQ
    $.
    $( [21-Jun-2004] $)

  $( Hilbert lattice contraposition law. $)
  chsscon2 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A C_ ( _|_ ` B ) <-> B C_ ( _|_ ` A ) ) ) $=
    ( cch wcel chil wss cort cfv wb chss occon3 syl2an ) ACDAEFBEFABGHFBAGHFIBC
    DAJBJABKL $.
    $( [21-Jun-2004] $)

  $( Hilbert lattice contraposition law for strict ordering. $)
  chpsscon3 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A C. B <-> ( _|_ ` B ) C. ( _|_ ` A ) ) ) $=
    ( cch wcel wa wss wn cort cfv wpss chsscon3 wb ancoms notbid anbi12d dfpss3
    3bitr4g ) ACDZBCDZEZABFZBAFZGZEBHIZAHIZFZUEUDFZGZEABJUDUEJTUAUFUCUHABKTUBUG
    SRUBUGLBAKMNOABPUDUEPQ $.
    $( [12-Jun-2004] $)

  $( Hilbert lattice contraposition law for strict ordering. $)
  chpsscon1 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( ( _|_ ` A ) C. B <-> ( _|_ ` B ) C. A ) ) $=
    ( cch wcel wa cort cfv wpss choccl chpsscon3 sylan wceq ococ adantr psseq2d
    wb bitrd ) ACDZBCDZEZAFGZBHZBFGZUAFGZHZUCAHRUACDSUBUEPAIUABJKTUDAUCRUDALSAM
    NOQ $.
    $( [12-Jun-2004] $)

  $( Hilbert lattice contraposition law for strict ordering. $)
  chpsscon2 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A C. ( _|_ ` B ) <-> B C. ( _|_ ` A ) ) ) $=
    ( cch wcel wa cort wpss wb choccl chpsscon3 sylan2 wceq ococ adantl psseq1d
    cfv bitrd ) ACDZBCDZEZABFPZGZUAFPZAFPZGZBUDGSRUACDUBUEHBIAUAJKTUCBUDSUCBLRB
    MNOQ $.
    $( [12-Jun-2004] $)

  $( Commutative law for Hilbert lattice join. $)
  chjcom $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH B ) = ( B vH A ) ) $=
    ( cch wcel csh chj co wceq chsh shjcom syl2an ) ACDAEDBEDABFGBAFGHBCDAIBIAB
    JK $.
    $( [12-Jun-2004] $)

  $( Hilbert lattice join is greater than or equal to its first argument. $)
  chub1 $p |- ( ( A e. CH /\ B e. CH ) -> A C_ ( A vH B ) ) $=
    ( cch wcel csh chj co wss chsh shub1 syl2an ) ACDAEDBEDAABFGHBCDAIBIABJK $.
    $( [12-Jun-2004] $)

  $( Hilbert lattice join is greater than or equal to its second argument. $)
  chub2 $p |- ( ( A e. CH /\ B e. CH ) -> A C_ ( B vH A ) ) $=
    ( cch wcel wa chj co chub1 chjcom sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
    $( [12-Jun-2004] $)

  $( Hilbert lattice join is the least upper bound of two elements. $)
  chlub $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
               ( ( A C_ C /\ B C_ C ) <-> ( A vH B ) C_ C ) ) $=
    ( cch wcel csh wss wa chj co wb chsh shlub syl3an2 syl3an1 ) ADEAFEZBDEZCDE
    ZACGBCGHABIJCGKZALQPBFERSBLABCMNO $.
    $( [12-Jun-2004] $)

  $( Add join to both sides of Hilbert lattice ordering. $)
  chlej1 $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\
                A C_ B ) -> ( A vH C ) C_ ( B vH C ) ) $=
    ( cch wcel csh wss chj co chsh shlej1 syl3anl ) ADEAFEBDEBFECDECFEABGACHIBC
    HIGAJBJCJABCKL $.
    $( [17-Jun-2007] $) $( [22-Jun-2004] $)

  $( Add join to both sides of Hilbert lattice ordering. $)
  chlej2 $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\
                A C_ B ) -> ( C vH A ) C_ ( C vH B ) ) $=
    ( cch wcel csh wss chj co chsh shlej2 syl3anl ) ADEAFEBDEBFECDECFEABGCAHICB
    HIGAJBJCJABCKL $.
    $( [4-Jan-2007] $) $( [22-Jun-2004] $)

  $( Hilbert lattice ordering in terms of join. $)
  chlejb1 $p |- ( ( A e. CH /\ B e. CH ) -> ( A C_ B <-> ( A vH B ) = B ) ) $=
    ( cch wcel wss chj co wceq wb c0h sseq1 oveq1 eqeq1d bibi12d sseq2 oveq2 id
    cif h0elch elimel eqeq12d chlejb1i dedth2h ) ACDZBCDZABEZABFGZBHZIUDAJRZBEZ
    UIBFGZBHZIUIUEBJRZEZUIUMFGZUMHZIABJJAUIHZUFUJUHULAUIBKUQUGUKBAUIBFLMNBUMHZU
    JUNULUPBUMUIOURUKUOBUMBUMUIFPURQUANUIUMAJCSTBJCSTUBUC $.
    $( [30-Jun-2004] $)

  $( Hilbert lattice ordering in terms of join. $)
  chlejb2 $p |- ( ( A e. CH /\ B e. CH ) -> ( A C_ B <-> ( B vH A ) = B ) ) $=
    ( cch wcel wa wss chj co wceq chlejb1 chjcom eqeq1d bitrd ) ACDBCDEZABFABGH
    ZBIBAGHZBIABJNOPBABKLM $.
    $( [2-Jul-2004] $)

  $( Equivalent expressions for "not less than" in the Hilbert lattice. $)
  chnle $p |- ( ( A e. CH /\ B e. CH ) ->
               ( -. B C_ A <-> A C. ( A vH B ) ) ) $=
    ( cch wcel wss wn chj co wpss wb c0h cif wceq sseq2 notbid id oveq1 bibi12d
    h0elch elimel psseq12d sseq1 oveq2 psseq2d chnlei dedth2h ) ACDZBCDZBAEZFZA
    ABGHZIZJBUGAKLZEZFZUMUMBGHZIZJUHBKLZUMEZFZUMUMURGHZIZJABKKAUMMZUJUOULUQVCUI
    UNAUMBNOVCAUMUKUPVCPAUMBGQUARBURMZUOUTUQVBVDUNUSBURUMUBOVDUPVAUMBURUMGUCUDR
    UMURAKCSTBKCSTUEUF $.
    $( [9-Jun-2004] $)

  $( The join of a closed subspace and its orthocomplement is all of Hilbert
     space. $)
  chjo $p |- ( A e. CH -> ( A vH ( _|_ ` A ) ) = ~H ) $=
    ( cch wcel cort cfv chj co chil wceq cif id fveq2 eqeq1d helch elimel chjoi
    oveq12d dedth ) ABCZAADEZFGZHISAHJZUBDEZFGZHIAHAUBIZUAUDHUEAUBTUCFUEKAUBDLQ
    MUBAHBNOPR $.
    $( [31-Oct-2005] $)

  $( Hilbert lattice absorption law.  From definition of lattice in [Kalmbach]
     p. 14. $)
  chabs1 $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH ( A i^i B ) ) = A ) $=
    ( cch wcel wa cin chj co wss ssid inss1 pm3.2i wb simpl chlub syl3anc mpbii
    chincl chub1 syldan eqssd ) ACDZBCDZEZAABFZGHZAUDAAIZUEAIZEZUFAIZUGUHAJABKL
    UDUBUECDZUBUIUJMUBUCNZABRZULAUEAOPQUBUCUKAUFIUMAUESTUA $.
    $( [15-Jun-2004] $)

  $( Hilbert lattice absorption law.  From definition of lattice in [Kalmbach]
     p. 14. $)
  chabs2 $p |- ( ( A e. CH /\ B e. CH ) -> ( A i^i ( A vH B ) ) = A ) $=
    ( cch wcel wa chj co cin wceq chub1 ssid jctil ssin sylib inss1 eqss sylibr
    wss ) ACDBCDEZAABFGZHZARZAUARZEUAAISUCUBSAARZATRZEUCSUEUDABJAKLAATMNATOLUAA
    PQ $.
    $( [16-Jun-2004] $)

  ${
    chabs.1 $e |- A e. CH $.
    chabs.2 $e |- B e. CH $.
    $( Hilbert lattice absorption law.  From definition of lattice in
       [Kalmbach] p. 14. $)
    chabs1i $p |- ( A vH ( A i^i B ) ) = A $=
      ( cch wcel cin chj co wceq chabs1 mp2an ) AEFBEFAABGHIAJCDABKL $.
      $( [10-Jun-2004] $)

    $( Hilbert lattice absorption law.  From definition of lattice in
       [Kalmbach] p. 14. $)
    chabs2i $p |- ( A i^i ( A vH B ) ) = A $=
      ( cch wcel chj co cin wceq chabs2 mp2an ) AEFBEFAABGHIAJCDABKL $.
      $( [16-Jun-2004] $)
  $}

  $( Idempotent law for Hilbert lattice join. $)
  chjidm $p |- ( A e. CH -> ( A vH A ) = A ) $=
    ( cch wcel chj co cin inidm oveq2i wceq chabs1 anidms syl5eqr ) ABCZAADEAAA
    FZDEZANAADAGHMOAIAAJKL $.
    $( [26-Jun-2004] $)

  ${
    chjidm.1 $e |- A e. CH $.
    $( Idempotent law for Hilbert lattice join. $)
    chjidmi $p |- ( A vH A ) = A $=
      ( cch wcel chj co wceq chjidm ax-mp ) ACDAAEFAGBAHI $.
      $( [15-Jun-2004] $)
  $}

  ${
    chj12.1 $e |- A e. CH $.
    chj12.2 $e |- B e. CH $.
    chj12.3 $e |- C e. CH $.
    $( A rearrangement of Hilbert lattice join. $)
    chj12i $p |- ( A vH ( B vH C ) ) = ( B vH ( A vH C ) ) $=
      ( chj co chjcomi oveq1i chjassi 3eqtr3i ) ABGHZCGHBAGHZCGHABCGHGHBACGHGHM
      NCGABDEIJABCDEFKBACEDFKL $.
      $( [29-Apr-2006] $)

    ${
      chj4.4 $e |- D e. CH $.
      $( Rearrangement of the join of 4 Hilbert lattice elements. $)
      chj4i $p |- ( ( A vH B ) vH ( C vH D ) ) =
                 ( ( A vH C ) vH ( B vH D ) ) $=
        ( chj co chj12i oveq2i chjcli chjassi 3eqtr4i ) ABCDIJZIJZIJACBDIJZIJZI
        JABIJPIJACIJRIJQSAIBCDFGHKLABPEFCDGHMNACREGBDFHMNO $.
        $( [29-Apr-2006] $)
    $}

    $( Hilbert lattice join distributes over itself. $)
    chjjdiri $p |- ( ( A vH B ) vH C ) = ( ( A vH C ) vH ( B vH C ) ) $=
      ( chj co chjidmi oveq2i chj4i eqtr3i ) ABGHZCCGHZGHMCGHACGHBCGHGHNCMGCFIJ
      ABCCDEFFKL $.
      $( [29-Apr-2006] $)
  $}

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm1 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A i^i B ) ) = ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) $=
    ( cch wcel cin cort cfv chj wceq chil cif ineq1 fveq2d fveq2 oveq1d eqeq12d
    co ineq2 helch elimel oveq2d chdmm1i dedth2h ) ACDZBCDZABEZFGZAFGZBFGZHQZIU
    DAJKZBEZFGZUKFGZUIHQZIUKUEBJKZEZFGZUNUPFGZHQZIABJJAUKIZUGUMUJUOVAUFULFAUKBL
    MVAUHUNUIHAUKFNOPBUPIZUMURUOUTVBULUQFBUPUKRMVBUIUSUNHBUPFNUAPUKUPAJCSTBJCST
    UBUC $.
    $( [21-Jun-2004] $)

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm2 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) i^i B ) ) = ( A vH ( _|_ ` B ) ) ) $=
    ( cch wcel wa cort cfv cin chj wceq choccl chdmm1 sylan adantr oveq1d eqtrd
    co ococ ) ACDZBCDZEZAFGZBHFGZUBFGZBFGZIQZAUEIQSUBCDTUCUFJAKUBBLMUAUDAUEISUD
    AJTARNOP $.
    $( [21-Jun-2004] $)

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm3 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A i^i ( _|_ ` B ) ) ) = ( ( _|_ ` A ) vH B ) ) $=
    ( cch wcel wa cort cfv cin chj wceq choccl chdmm1 sylan2 ococ adantl oveq2d
    co eqtrd ) ACDZBCDZEZABFGZHFGZAFGZUBFGZIQZUDBIQTSUBCDUCUFJBKAUBLMUAUEBUDITU
    EBJSBNOPR $.
    $( [21-Jun-2004] $)

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm4 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) i^i ( _|_ ` B ) ) ) = ( A vH B ) ) $=
    ( cch wcel wa cort cfv cin chj wceq choccl chdmm2 sylan2 ococ adantl oveq2d
    co eqtrd ) ACDZBCDZEZAFGBFGZHFGZAUBFGZIQZABIQTSUBCDUCUEJBKAUBLMUAUDBAITUDBJ
    SBNOPR $.
    $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj1 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A vH B ) ) = ( ( _|_ ` A ) i^i ( _|_ ` B ) ) ) $=
    ( cch wcel wa cort cfv cin chj chdmm4 fveq2d wceq choccl chincl syl2an ococ
    co syl eqtr3d ) ACDZBCDZEZAFGZBFGZHZFGZFGZABIQZFGUEUBUFUHFABJKUBUECDZUGUELT
    UCCDUDCDUIUAAMBMUCUDNOUEPRS $.
    $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj2 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) vH B ) ) = ( A i^i ( _|_ ` B ) ) ) $=
    ( cch wcel wa cort cfv chj cin wceq choccl chdmj1 sylan ineq1d adantr eqtrd
    co ococ ) ACDZBCDZEAFGZBHQFGZUAFGZBFGZIZAUDIZSUACDTUBUEJAKUABLMSUEUFJTSUCAU
    DARNOP $.
    $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj3 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A vH ( _|_ ` B ) ) ) = ( ( _|_ ` A ) i^i B ) ) $=
    ( cch wcel wa cort cfv chj cin wceq choccl chdmj1 sylan2 ococ adantl ineq2d
    co eqtrd ) ACDZBCDZEZABFGZHQFGZAFGZUBFGZIZUDBITSUBCDUCUFJBKAUBLMUAUEBUDTUEB
    JSBNOPR $.
    $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj4 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) = ( A i^i B ) ) $=
    ( cch wcel wa cort cfv chj cin wceq choccl chdmj2 sylan2 ococ adantl ineq2d
    co eqtrd ) ACDZBCDZEZAFGBFGZHQFGZAUBFGZIZABITSUBCDUCUEJBKAUBLMUAUDBATUDBJSB
    NOPR $.
    $( [21-Jun-2004] $)

  $( Associative law for Hilbert lattice join.  From definition of lattice in
     [Kalmbach] p. 14. $)
  chjass $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
                ( ( A vH B ) vH C ) = ( A vH ( B vH C ) ) ) $=
    ( cch wcel chj wceq chil cif oveq1 oveq1d eqeq12d oveq2 oveq2d helch elimel
    co chjassi dedth3h ) ADEZBDEZCDEZABFQZCFQZABCFQZFQZGTAHIZBFQZCFQZUGUEFQZGUG
    UABHIZFQZCFQZUGUKCFQZFQZGULUBCHIZFQZUGUKUPFQZFQZGABCHHHAUGGZUDUIUFUJUTUCUHC
    FAUGBFJKAUGUEFJLBUKGZUIUMUJUOVAUHULCFBUKUGFMKVAUEUNUGFBUKCFJNLCUPGZUMUQUOUS
    CUPULFMVBUNURUGFCUPUKFMNLUGUKUPAHDOPBHDOPCHDOPRS $.
    $( [10-Jun-2004] $)

  $( A rearrangement of Hilbert lattice join. $)
  chj12 $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
              ( A vH ( B vH C ) ) = ( B vH ( A vH C ) ) ) $=
    ( cch wcel w3a chj co wceq chjcom 3adant3 oveq1d chjass 3com12 3eqtr3d ) AD
    EZBDEZCDEZFZABGHZCGHBAGHZCGHZABCGHGHBACGHGHZSTUACGPQTUAIRABJKLABCMQPRUBUCIB
    ACMNO $.
    $( [15-Jun-2006] $)

  $( Rearrangement of the join of 4 Hilbert lattice elements. $)
  chj4 $p |- ( ( ( A e. CH /\ B e. CH ) /\ ( C e. CH /\ D e. CH ) ) ->
              ( ( A vH B ) vH ( C vH D ) ) = ( ( A vH C ) vH ( B vH D ) ) ) $=
    ( cch wcel wa chj wceq chj12 3expb adantll oveq2d chjcl chjass 3expa sylan2
    co an4s 3eqtr4d ) AEFZBEFZGZCEFZDEFZGZGZABCDHRZHRZHRZACBDHRZHRZHRZABHRUHHRZ
    ACHRUKHRZUGUIULAHUBUFUIULIZUAUBUDUEUPBCDJKLMUFUCUHEFZUNUJIZCDNUAUBUQURABUHO
    PQUAUDUBUEUOUMIZUBUEGUAUDGUKEFZUSBDNUAUDUTUSACUKOPQST $.
    $( [15-Jun-2006] $)

  ${
    ledi.1 $e |- A e. CH $.
    ledi.2 $e |- B e. CH $.
    ledi.3 $e |- C e. CH $.
    $( An ortholattice is distributive in one ordering direction. $)
    ledii $p |- ( ( A i^i B ) vH ( A i^i C ) ) C_ ( A i^i ( B vH C ) ) $=
      ( cin chj co wss inss1 chincli chlubii mp2an inss2 chlej12i ssini ) ABGZA
      CGZHIZABCHIZRAJSAJTAJABKACKRSAABDELZACDFLZDMNRBJSCJTUAJABOACORBSCUBEUCFPN
      Q $.
      $( [6-Aug-2004] $)

    $( An ortholattice is distributive in one ordering direction. $)
    lediri $p |- ( ( A i^i C ) vH ( B i^i C ) ) C_ ( ( A vH B ) i^i C ) $=
      ( cin chj co ledii incom oveq12i 3sstr4i ) CAGZCBGZHICABHIZGACGZBCGZHIPCG
      CABFDEJQNROHACKBCKLPCKM $.
      $( [27-Apr-2006] $)

    $( An ortholattice is distributive in one ordering direction (join
       version). $)
    lejdii $p |- ( A vH ( B i^i C ) ) C_ ( ( A vH B ) i^i ( A vH C ) ) $=
      ( cin chj co wss wa chincli chjcli chlubi bicomi chub1i ssini inss1 sstri
      chub2i inss2 mpbir2an ) ABCGZHIABHIZACHIZGZJZAUFJZUCUFJZUHUIKUGAUCUFDBCEF
      LUDUEABDEMACDFMLNOAUDUEABDEPACDFPQUCUDUEUCBUDBCRBAEDTSUCCUEBCUACAFDTSQUB
      $.
      $( [27-Apr-2006] $)

    $( An ortholattice is distributive in one ordering direction (join
       version). $)
    lejdiri $p |- ( ( A i^i B ) vH C ) C_ ( ( A vH C ) i^i ( B vH C ) ) $=
      ( cin chj co lejdii chincli chjcomi ineq12i 3sstr4i ) CABGZHICAHIZCBHIZGO
      CHIACHIZBCHIZGCABFDEJOCABDEKFLRPSQACDFLBCEFLMN $.
      $( [27-Apr-2006] $)
  $}

  $( An ortholattice is distributive in one ordering direction. $)
  ledi $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
              ( ( A i^i B ) vH ( A i^i C ) ) C_ ( A i^i ( B vH C ) ) ) $=
    ( cch wcel cin chj co wss c0h cif ineq1 oveq12d sseq12d ineq2 oveq1d ineq2d
    wceq h0elch elimel oveq1 oveq2d oveq2 ledii dedth3h ) ADEZBDEZCDEZABFZACFZG
    HZABCGHZFZIUFAJKZBFZUNCFZGHZUNULFZIUNUGBJKZFZUPGHZUNUSCGHZFZIUTUNUHCJKZFZGH
    ZUNUSVDGHZFZIABCJJJAUNRZUKUQUMURVIUIUOUJUPGAUNBLAUNCLMAUNULLNBUSRZUQVAURVCV
    JUOUTUPGBUSUNOPVJULVBUNBUSCGUAQNCVDRZVAVFVCVHVKUPVEUTGCVDUNOUBVKVBVGUNCVDUS
    GUCQNUNUSVDAJDSTBJDSTCJDSTUDUE $.
    $( [14-Jun-2006] $)

