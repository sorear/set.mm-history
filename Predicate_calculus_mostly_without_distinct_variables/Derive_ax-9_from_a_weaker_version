$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Derive ~ ax-9 from a weaker version
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x w $.  $d w z $.
    a9wa9lem1.1 $e |- -. A. w -. w = x $.
    $( Lemma for ~ a9wa9 .  Similar to ~ equid , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem1 $p |- x = x $=
      ( weq wn wal ax-8 pm2.43i con3i alimi mto ax-17 mt3 ) AADZNEZBFZPBADZEZBF
      CORBQNQNBAAGHIJKOBLM $.
      $( [12-Nov-2013] $)

    $( Lemma for ~ a9wa9 .  Similar to ~ equcomi , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem2 $p |- ( x = y -> y = x ) $=
      ( weq a9wa9lem1 ax-8 mpi ) ABEAAEBAEACDFABAGH $.
      $( [12-Nov-2013] $)

    a9wa9lem3.2 $e |- -. A. w -. w = z $.
    $( Lemma for ~ a9wa9 .  Similar to ~ equequ2 , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem3 $p |- ( x = y -> ( z = x <-> z = y ) ) $=
      ( weq wi a9wa9lem2 ax-8 syl com12 syl2im impbid ) ABGZCAGZCBGZPOQPACGOQHC
      ADFIACBJKLOBAGZQBCGZPABDEICBDFISRPBCAJLMN $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d x w $.  $d w ph $.
    a9wa9lem4.1 $e |- -. A. w -. w = x $.
    a9wa9lem4.2 $e |- -. A. x -. x = w $.
    $( Lemma for ~ a9wa9 .  Similar to ~ ax4 , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem4 $p |- ( A. x ph -> ph ) $=
      ( wal wi weq wn ax-17 a9wa9lem2 ax-11 syl2im con2 al2imi mtoi syl6 con4d
      con3i 19.21ai mt3 ) ABFZAGZCBHZIZCFDUCIZUECUFCJUDUCUDAUBUDAIZBCHZUGGZBFZU
      BIUDUHUGUGCFUJCBBEKUGCJUGBCLMUJUBUHIZBFEUIAUKBUHANOPQRSTUA $.
      $( [12-Nov-2013] $)

    $( Lemma for ~ a9wa9 .  Similar to ~ hba1 , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem5 $p |- ( A. x ph -> A. x A. x ph ) $=
      ( wal wn a9wa9lem4 con2i ax-6 con1i alimi 3syl ) ABFZNGZBFZGZQBFNBFPNOBCD
      EHIOBJQNBNPABJKLM $.
      $( [12-Nov-2013] $)

    a9wa9lem6.3 $e |- ( ph -> A. x ph ) $.
    $( Lemma for ~ a9wa9 .  Similar to ~ hbn , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem6 $p |- ( -. ph -> A. x -. ph ) $=
      ( wn wal a9wa9lem4 con3i ax-6 19.21ai syl ) AGZABHZGZNBHOAABCDEIJPNBABKAO
      FJLM $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d x w $.  $d w ph $.  $d w ps $.
    a9wa9lem7.a $e |- -. A. w -. w = x $.
    a9wa9lem7.b $e |- -. A. x -. x = w $.
    a9wa9lem7.1 $e |- ( ph -> A. x ph ) $.
    a9wa9lem7.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    a9wa9lem7.3 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Lemma for ~ a9wa9 .  Similar to ~ hbimd , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem7 $p |- ( ph -> ( ( ps -> ch ) -> A. x ( ps -> ch ) ) ) $=
      ( wi wal wn 19.21ai a9wa9lem4 ax-6 nsyl4 con1i con3 al2imi syl2im pm2.21
      alimi syl6com ax-1 ja com12 ) BCKZAUHDLZBCAUIKABMZUJDLZUIABBDLZKZDLUJULMZ
      DLZUKAUMDHINUOBULBUOBDEFGOBDPQRUMUNUJDBULSTUAUJUHDBCUBUCUDACCDLUIJCUHDCBU
      EUCUDUFUG $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d x w $.  $d w ph $.  $d w ps $.
    a9wa9lem8.a $e |- -. A. w -. w = x $.
    a9wa9lem8.b $e |- -. A. x -. x = w $.
    a9wa9lem8.1 $e |- ( ph -> A. x ph ) $.
    a9wa9lem8.2 $e |- ( ps -> A. x ps ) $.
    $( Lemma for ~ a9wa9 .  Similar to ~ hban , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem8 $p |- ( ( ph /\ ps ) -> A. x ( ph /\ ps ) ) $=
      ( wa wn wi df-an wal a9wa9lem6 pm2.21 19.21ai ax-1 ja hbxfrbi ) ABIABJZKZ
      JCABLUACDEFATUACMAJUACACDEFGNATOPTUACBCDEFHNTAQPRNS $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d x w z $.  $d y w z $.  $d w x ph $.  $d w z ps $.
    a9wa9lem9.a $e |- -. A. w -. w = x $.
    a9wa9lem9.b $e |- -. A. x -. x = w $.
    a9wa9lem9.x $e |- -. A. z -. z = y $.
    a9wa9lem9.y $e |- -. A. w -. w = z $.
    a9wa9lem9.z $e |- -. A. z -. z = w $.
    a9wa9lem9.3 $e |- ( z = y -> ( ph <-> ps ) ) $.
    $( Lemma for ~ a9wa9 .  Similar to ~ dvelimfALT , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem9 $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( weq wi wal wn ax-17 a1d 19.21ai a9wa9lem4 syl5ibr a2i alimi syl
      a9wa9lem5 ax-10 a9wa9lem2 ax-11 3syl pm2.27 al2imi sylsyld alequcoms
      syl5 wa ax-6 con3i nalequcoms a9wa9lem8 ax-12 imp a1i a9wa9lem7 hbald ex
      pm2.61i syl6bi con3 mtoi nsyl4 syl56 ) BEDMZANZEOZCDMZCOPZVNCOZBCOBVLBEOZ
      NZEOZVNBVSEBEQZBVRVLWARSVSVMEVLVRAVRAVLBBEFJKTZLUAUBUCUDCEMZCOZVPVNVQNZNW
      DWEVPVNVNEOZWDVQVMEFJKUEWFVQNECECMZEOZWDWFWCVNNZCOZVQECUFWHWGWCWFWJNWGEFJ
      KTECFJUGVNCEUHUIWCWIVNCWCVNUJUKULUMUNRWDPZVPWEWKVPUOZVMCEWKVPEFJKWKEOECWH
      PWKEWGEUPWDWHCEUFUQSURVPEQUSWLVLACFGHWKVPCFGHWCCUPVOCUPUSWKVPVLVLCONEDCUT
      VAAACONWLACQVBVCVDVEVFVNBCVNVTVRPZEOZPBVMVSEVLAVRVLABVRLWAVGUBUCVTWNVLPZE
      OIVSWMWOEVLVRVHUKVIVRBWNWBBEUPVJUIUCVK $.
      $( [12-Nov-2013] $)

    $( Lemma for ~ a9wa9 .  Similar to ~ dvelimfALT , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem9OLD $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( weq wal wn wi a9wa9lem5 ax-10 a9wa9lem4 a9wa9lem2 ax-11 3syl pm2.27
      al2imi sylsyld alequcoms syl5 a1d wa ax-6 con3i 19.21ai nalequcoms ax-17
      a9wa9lem8 ax-12 imp a1i a9wa9lem7 hbald ex pm2.61i impbii syl6bbr
      pm5.74i albii con3 mtoi nsyl4 syl bitr4i 3imtr3g ) CDMZCNOZEDMZAPZENZVQCN
      ZBBCNCEMZCNZVNVQVRPZPVTWAVNVQVQENZVTVRVPEFJKQWBVRPECECMZENZVTWBVSVQPZCNZV
      RECRWDWCVSWBWFPWCEFJKSECFJTVQCEUAUBVSWEVQCVSVQUCUDUEUFUGUHVTOZVNWAWGVNUIZ
      VPCEWGVNEFJKWGENECWDOWGEWCEUJVTWDCERUKULUMVNEUNUOWHVOACFGHWGVNCFGHVSCUJVM
      CUJUOWGVNVOVOCNPEDCUPUQAACNPWHACUNURUSUTVAVBVQVOBENZPZENZBVPWJEVOAWIVOABW
      ILWIBBEFJKSZBEUNZVCVDVEVFBWKBWJEWMBWIVOWMUHULWKWIOZENZOBWKWOVOOZENIWJWNWP
      EVOWIVGUDVHWIBWOWLBEUJVIVJVCVKZVQBCWQVFVL $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d x z w v $.  $d y z w v $.
    a9wa9.a $e |- -. A. w -. w = x $.
    a9wa9.b $e |- -. A. x -. x = w $.
    a9wa9.x $e |- -. A. z -. z = y $.
    a9wa9.y $e |- -. A. w -. w = z $.
    a9wa9.w $e |- -. A. x -. x = z $.
    a9wa9.q $e |- -. A. v -. v = y $.
    a9wa9.r $e |- -. A. w -. w = v $.
    a9wa9.s $e |- -. A. v -. v = w $.
    $( Derive ~ ax-9 (which has no distinct variable requirement) from a weaker
       version that requires that its two variables be distinct.  The
       hypotheses are the instances of the weaker version that we need.
       Neither ~ ax-9 nor ~ ax-4 (which can be derived from ~ ax-9 ) is used by
       the proof. $)
    a9wa9 $p |- -. A. x -. x = y $=
      ( weq wex wn wal a9wa9lem4 alnex sylbir con4i syl wi ax-17 a9wa9lem3
      a9wa9lem9 df-ex mpbir a9wa9lem5 wb exbid mpbii syl6com con3i 19.21ai mt3
      pm2.61i mpbi ) ABNZAOZUSPZAQZPUSAQZUTVCUSUTUSADFGRUTUSUTPVBVAUSASVAADFGRT
      UAUBVCPZUTUCZCBNZPZCQHVEPZVGCVHCUDVFVEVDVFVFAQZUTCENVFABEDFGKLMEBCDLIUEUF
      VIACNZAOZUTVKVJPAQPJVJAUGUHVIVJUSAVFADFGUIVIVFVJUSUJVFADFGRCBADIFUEUBUKUL
      UMUNUOUPUQUSAUGUR $.
      $( [12-Nov-2013] $)
  $}


