$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Derive ~ ax-9 from a weaker version
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x w $.  $d w z $.
    a9wa9lem1.1 $e |- -. A. w -. w = x $.
    $( Lemma for ~ a9wa9 .  Similar to ~ equcomi , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem1 $p |- ( x = y -> y = x ) $=
      ( weq wn wal cv wceq ax-8 pm2.43i con3i alimi mto ax-17 mt3 mpi ) ABEAAEZ
      BAERRFZCGZTCHAHIZFZCGDSUBCUARUARCAAJKLMNSCOPABAJQ $.
      $( [12-Nov-2013] $)

    a9wa9lem2.2 $e |- -. A. w -. w = z $.
    $( Lemma for ~ a9wa9 .  Similar to ~ equequ2 , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem2 $p |- ( x = y -> ( z = x <-> z = y ) ) $=
      ( weq wi a9wa9lem1 ax-8 syl com12 syl2im impbid ) ABGZCAGZCBGZPOQPACGOQHC
      ADFIACBJKLOBAGZQBCGZPABDEICBDFISRPBCAJLMN $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d x w $.  $d w ph $.
    a9wa9lem3.1 $e |- -. A. w -. w = x $.
    a9wa9lem3.2 $e |- -. A. x -. x = w $.
    $( Lemma for ~ a9wa9 .  Similar to ~ ax4 , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem3 $p |- ( A. x ph -> ph ) $=
      ( wal wi weq wn ax-17 a9wa9lem1 ax-11 syl2im con2 al2imi mtoi syl6 con4d
      con3i 19.21ai mt3 ) ABFZAGZCBHZIZCFDUCIZUECUFCJUDUCUDAUBUDAIZBCHZUGGZBFZU
      BIUDUHUGUGCFUJCBBEKUGCJUGBCLMUJUBUHIZBFEUIAUKBUHANOPQRSTUA $.
      $( [12-Nov-2013] $)

    $( Lemma for ~ a9wa9 .  Similar to ~ hba1 , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem4 $p |- ( A. x ph -> A. x A. x ph ) $=
      ( wal wn a9wa9lem3 con2i ax-6 con1i alimi 3syl ) ABFZNGZBFZGZQBFNBFPNOBCD
      EHIOBJQNBNPABJKLM $.
      $( [12-Nov-2013] $)

    a9wa9lem5.3 $e |- ( ph -> A. x ph ) $.
    $( Lemma for ~ a9wa9 .  Similar to ~ hbn , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem5 $p |- ( -. ph -> A. x -. ph ) $=
      ( wn wal a9wa9lem3 con3i ax-6 19.21ai syl ) AGZABHZGZNBHOAABCDEIJPNBABKAO
      FJLM $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d x w $.  $d w ph $.  $d w ps $.
    a9wa9lem6.1 $e |- -. A. w -. w = x $.
    a9wa9lem6.2 $e |- -. A. x -. x = w $.
    a9wa9lem6.3 $e |- ( ph -> A. x ph ) $.
    a9wa9lem6.4 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    a9wa9lem6.5 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Lemma for ~ a9wa9 .  Similar to ~ hbimd , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem6 $p |- ( ph -> ( ( ps -> ch ) -> A. x ( ps -> ch ) ) ) $=
      ( wi wal wn 19.21ai a9wa9lem3 ax-6 nsyl4 con1i con3 al2imi syl2im pm2.21
      alimi syl6com ax-1 ja com12 ) BCKZAUHDLZBCAUIKABMZUJDLZUIABBDLZKZDLUJULMZ
      DLZUKAUMDHINUOBULBUOBDEFGOBDPQRUMUNUJDBULSTUAUJUHDBCUBUCUDACCDLUIJCUHDCBU
      EUCUDUFUG $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d x w $.  $d w ph $.  $d w ps $.
    a9wa9lem7.1 $e |- -. A. w -. w = x $.
    a9wa9lem7.2 $e |- -. A. x -. x = w $.
    a9wa9lem7.3 $e |- ( ph -> A. x ph ) $.
    a9wa9lem7.4 $e |- ( ps -> A. x ps ) $.
    $( Lemma for ~ a9wa9 .  Similar to ~ hban , without using ~ ax-9 or
       ~ ax-4 . $)
    a9wa9lem7 $p |- ( ( ph /\ ps ) -> A. x ( ph /\ ps ) ) $=
      ( wa wn wi df-an wal a9wa9lem5 pm2.21 19.21ai ax-1 ja hbxfrbi ) ABIABJZKZ
      JCABLUACDEFATUACMAJUACACDEFGNATOPTUACBCDEFHNTAQPRNS $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d x v w z $.  $d y v w z $.  $d w x ph $.  $d w z ps $.
    a9wa9.1 $e |- -. A. w -. w = x $.
    a9wa9.2 $e |- -. A. x -. x = w $.
    a9wa9.3 $e |- -. A. z -. z = y $.
    a9wa9.4 $e |- -. A. w -. w = z $.
    a9wa9.5 $e |- -. A. z -. z = w $.
    ${
      a9wa9lem8.6 $e |- ( z = y -> ( ph <-> ps ) ) $.
      $( Lemma for ~ a9wa9 .  Similar to ~ dvelimfALT , without using ~ ax-9 or
         ~ ax-4 . $)
      a9wa9lem8 $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
        ( weq wi wal wn ax-17 a1d 19.21ai a9wa9lem3 syl5ibr a2i alimi syl
        a9wa9lem4 ax-11 pm2.27 al2imi syld syl5 wa ax-6 ax-10 con3i nalequcoms
        a9wa9lem7 ax-12 imp a17d a9wa9lem6 hbald ex pm2.61i biimpd con3 mtoi
        con1i 3syl syl56 ) BEDMZANZEOZCDMZCOPZVLCOZBCOBVJBEOZNZEOVLBVQEBEQZBVPV
        JVRRSVQVKEVJVPAVPAVJBBEFJKTLUAUBUCUDCEMZCOZVNVLVONZNVTWAVNVLVLEOZVTVOVK
        EFJKUEVTWBVSVLNZCOZVOVTVSWBWDNVSCFGHTVLCEUFUDVSWCVLCVSVLUGUHUIUJRVTPZVN
        WAWEVNUKZVKCEWEVNEFJKWEEOECECMZEOZPWEEWGEULVTWHCEUMUNSUOVNEQUPWFVJACFGH
        WEVNCFGHVSCULVMCULUPWEVNVJVJCONEDCUQURWFACUSUTVAVBVCVLBCVLVJBNZEOZBPZEO
        ZPBVKWIEVJABVJABLVDUBUCWJWLVJPZEOIWIWKWMEVJBVEUHVFBWLWKEQVGVHUCVI $.
        $( [12-Nov-2013] $)
    $}

    a9wa9.6 $e |- -. A. v -. v = y $.
    a9wa9.7 $e |- -. A. w -. w = v $.
    a9wa9.8 $e |- -. A. x -. x = v $.
    $( Derive ~ ax-9 (which has no distinct variable requirement) from a weaker
       version that requires that its two variables be distinct.  The
       hypotheses are the instances of the weaker version that we need.
       Neither ~ ax-9 nor ~ ax-4 (which can be derived from ~ ax-9 ) is used by
       the proof.  Note that every other predicate calculus axiom is used by
       the proof. $)
    a9wa9 $p |- -. A. x -. x = y $=
      ( cv wceq wal wn a9wa9lem3 con2i syl wi ax-17 a9wa9lem2 a9wa9lem8
      a9wa9lem4 wb notbid albid mpbii syl6com con3i 19.21ai mt3 pm2.61i ) ANZBN
      ZOZAPZUQQZAPZQZURUQVAUQADFGRUTUQUSADFGRSTURQZVAUAZENZUPOZQZEPKVCQZVFEVGEU
      BVEVCVBVEVEAPZVAVDCNOVEABCDFGHIJCBEDILUCUDVHUOVDOZQZAPZQVAMVHVKUTVHVJUSAV
      EADFGUEVHVIUQVHVEVIUQUFVEADFGREBADLFUCTUGUHUGUIUJUKULUMUN $.
      $( [12-Nov-2013] $)
  $}


