$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Continuous function-builders
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w y z A $.  $d w z B $.  $d w C $.  $d y z D $.  $d x y K $.  $d x ph $.
    $d x y L $.  $d x y z M $.  $d w x y z X $.  $d w x y z Y $.  $d x y z Z $.
    $d x y F $.  $d x y P $.
    cnmptid.x $e |- X = U. J $.
    cnmptid.j $e |- ( ph -> J e. Top ) $.
    $( The identity function is continuous.  (Contributed by Mario Carneiro,
       5-May-2014.) $)
    cnmptid $p |- ( ph -> ( x e. X |-> x ) e. ( J Cn J ) ) $=
      ( vy cv cmpt cid cres ccn co cvv wceq copab eqcom opabbii ax-mp wcel mptv
      dfid3 3eqtr4i reseq1 wss ssv resmpt eqtri ctop idcn syl syl5eqelr ) ABDBH
      ZIZJDKZCCLMZUOBNUMIZDKZUNJUQOUOUROUMGHZOZBGPUSUMOZBGPJUQUTVABGUMUSQRBGUBB
      GUMUAUCJUQDUDSDNUEURUNODUFBNDUMUGSUHACUITUOUPTFCDEUJUKUL $.
      $( [5-May-2014] $)

    cnmptc.y $e |- Y = U. K $.
    cnmptc.k $e |- ( ph -> K e. Top ) $.
    ${
      cnmptc.p $e |- ( ph -> P e. Y ) $.
      $( A constant function is continuous.  (Contributed by Mario Carneiro,
         5-May-2014.) $)
      cnmptc $p |- ( ph -> ( x e. X |-> P ) e. ( J Cn K ) ) $=
        ( vy ctop wcel csn cmpt wf wceq cv co wa copab df-mpt fconstopab eqtr4i
        ccn cxp wb fconst2g syl mpbiri cnconst syl22anc ) ADNOENOCGOZFCPZBFCQZR
        ZUQDEUGUAOIKLAURUQFUPUHZSZUQBTFOMTCSUBBMUCUSBMFCUDBMFCUEUFAUOURUTUILFCG
        UQUJUKULCUQDEFGHJUMUN $.
        $( [5-May-2014] $)
    $}

    cnmpt11.l $e |- ( ph -> L e. Top ) $.
    cnmpt11.a $e |- ( ph -> ( x e. X |-> A ) e. ( J Cn K ) ) $.
    ${
      $d x B $.  $d y z C $.
      cnmpt11.b $e |- ( ph -> ( y e. Y |-> B ) e. ( K Cn L ) ) $.
      cnmpt11.c $e |- ( y = A -> B = C ) $.
      $( The composition of continuous functions is continuous.  (Contributed
         by Mario Carneiro, 5-May-2014.) $)
      cnmpt11 $p |- ( ph -> ( x e. X |-> C ) e. ( J Cn L ) ) $=
        ( wcel vz vw cmpt ccom ccn co wceq cv cfv wral wa simpr wf ctop syl3anc
        eqid fmpt sylibr r19.21bi fvmpt2 syl2anc fveq2d cuni adantr eleq1d sylc
        cnf rcla4v fvmptg eqtrd funmpt a1i fvco3 3eqtr4d ralrimiva ax-17 hbmpt1
        wfun hbco hbfv hbeq fveq2 eqeq12d cbvral sylib wfn wb fco ffn syl fmptd
        eqfnfv2 mpbird cnco syl32anc eqeltrrd ) ACKEUCZBJDUCZUDZBJFUCZGIUEUFZAW
        SWTUGZUAUHZWSUIZXCWTUIZUGZUAJUJZABUHZWSUIZXHWTUIZUGZBJUJXGAXKBJAXHJTZUK
        ZXHWRUIZWQUIZFXIXJXMXODWQUIZFXMXNDWQXMXLDKTZXNDUGAXLULZAXQBJAJKWRUMZXQB
        JUJAGUNTZHUNTZWRGHUEUFTZXSMOQWRGHJKLNVGUOZBJKDWRWRUPZUQURUSZBJDKWRYDUTV
        AVBXMXQFIVCZTZXPFUGYEXMXQEYFTZCKUJZYGYEAYIXLAKYFWQUMZYIAYAIUNTZWQHIUEUF
        TZYJOPRWQHIKYFNYFUPVGUOZCKYFEWQWQUPZUQURVDYHYGCDKCUHDUGEFYFSVEVHVFZCDEF
        KYFWQSYNVIVAVJXMWQVRZXSXLXIXOUGYPXMCKEVKVLAXSXLYCVDXRJKXHWQWRVMUOXMXLYG
        XJFUGXRYOBJFYFWTWTUPZUTVAVNVOXKXFBUAJXKUAVPBUBUBXDXEBUBXCWSBUBWQWRUBUHZ
        WQTBVPBUBJDVQVSYRXCTBVPZVTBUBXCWTBUBJFVQYSVTWAXHXCUGXIXDXJXEXHXCWSWBXHX
        CWTWBWCWDWEAWSJWFZWTJWFZXBXGWGAJYFWSUMZYTAYJXSUUBYMYCJKYFWQWRWHVAJYFWSW
        IWJAJYFWTUMUUAABJFYFWTYOYQWKJYFWTWIWJUAJWSWTWLVAWMAXTYAYKYBYLWSXATMOPQR
        WRWQGHIWNWOWP $.
        $( [5-May-2014] $)
    $}

    ${
      cnmpt11f.f $e |- ( ph -> F e. ( K Cn L ) ) $.
      $( The composition of continuous functions is continuous.  (Contributed
         by Mario Carneiro, 5-May-2014.) $)
      cnmpt11f $p |- ( ph -> ( x e. X |-> ( F ` A ) ) e. ( J Cn L ) ) $=
        ( vy cfv ctop wcel cv cmpt ccn co wfn wceq cuni wf eqid cnf syl3anc ffn
        syl dffn5v sylib eqeltrrd fveq2 cnmpt11 ) ABQCQUAZDRZCDREFGHIJKLMNOADQI
        UTUBZFGUCUDZADIUEZDVAUFAIGUGZDUHZVCAFSTGSTDVBTVEMNPDFGIVDLVDUIUJUKIVDDU
        LUMQIDUNUOPUPUSCDUQUR $.
        $( [5-May-2014] $)
    $}

    $d y B $.  $d x C $.
    cnmpt1t.b $e |- ( ph -> ( x e. X |-> B ) e. ( J Cn L ) ) $.
    $( The composition of continuous functions is continuous.  (Contributed by
       Mario Carneiro, 5-May-2014.) $)
    cnmpt1t $p |- ( ph ->
                    ( x e. X |-> <. A , B >. ) e. ( J Cn ( K tX L ) ) ) $=
      ( vy vz cmpt wcel cv cfv cop ctx co ccn wa wceq simpr wf wral cnf syl3anc
      ctop eqid fmpt sylibr r19.21bi fvmpt2 syl2anc cuni mpteq2dva ax-17 hbmpt1
      opeq12d hbfv hbop fveq2 cbvmpt txcnmpt syl32anc eqeltrrd ) ABHBUAZBHCSZUB
      ZVMBHDSZUBZUCZSZBHCDUCZSEFGUDUEUFUEZABHVRVTAVMHTZUGZVOCVQDWCWBCITZVOCUHAW
      BUIZAWDBHAHIVNUJZWDBHUKAEUNTZFUNTZVNEFUFUETZWFKMOVNEFHIJLULUMBHICVNVNUOZU
      PUQURBHCIVNWJUSUTWCWBDGVAZTZVQDUHWEAWLBHAHWKVPUJZWLBHUKAWGGUNTZVPEGUFUETZ
      WMKNPVPEGHWKJWKUOULUMBHWKDVPVPUOZUPUQURBHDWKVPWPUSUTVEVBAWHWNWGWIWOVSWATM
      NKOPQFGEVNVPVSHJBQRHVRQUAZVNUBZWQVPUBZUCRUAZVRTQVCBRWRWSBRWQVNBRHCVDWTWQT
      BVCZVFBRWQVPBRHDVDXAVFVGVMWQUHVOWRVQWSVMWQVNVHVMWQVPVHVEVIVJVKVL $.
      $( [5-May-2014] $)

    cnmpt12.m $e |- ( ph -> M e. Top ) $.
    ${
      $d y B $.
      cnmpt12f.f $e |- ( ph -> F e. ( ( K tX L ) Cn M ) ) $.
      $( The composition of continuous functions is continuous.  (Contributed
         by Mario Carneiro, 5-May-2014.) $)
      cnmpt12f $p |- ( ph -> ( x e. X |-> ( A F B ) ) e. ( J Cn M ) ) $=
        ( co cmpt cop cfv ccn df-ov mpteq2i cuni eqid ctop wcel syl2anc cnmpt1t
        ctx txtop cnmpt11f syl5eqel ) ABJCDEUAZUBBJCDUCZEUDZUBFIUEUABJURUTCDEUF
        UGABUSEFGHUNUAZIJVAUHZLMVBUIAGUJUKHUJUKVAUJUKOPGHUOULSABCDFGHJKLMNOPQRU
        MTUPUQ $.
        $( [5-May-2014] $)
    $}

    cnmpt12.z $e |- Z = U. L $.
    cnmpt12.c $e |- ( ph ->
                      ( y e. Y , z e. Z |-> C ) e. ( ( K tX L ) Cn M ) ) $.
    cnmpt12.d $e |- ( ( y = A /\ z = B ) -> C = D ) $.
    $( The composition of continuous functions is continuous. $)
    cnmpt12 $p |- ( ph -> ( x e. X |-> D ) e. ( J Cn M ) ) $=
      ( cmpt2 co cmpt ccn cv wcel wa cuni wceq wf wral ctop syl3anc eqid sylibr
      cnf fmpt r19.21bi wi wal jca cxp ctx txtop syl2anc txuni feq2d fmpt2 r2al
      mpbird sylib adantr eleq1 bi2anan9 eleq1d imbi12d cla42gv syl3c mpteq2dva
      ovmpt2ga cnmpt12f eqeltrrd ) ABMEFCDNOGUGZUHZUIBMHUIILUJUHABMWJHABUKMULZU
      MZENULZFOULZHLUNZULZWJHUOAWMBMAMNBMEUIZUPZWMBMUQAIURULZJURULZWQIJUJUHULWR
      QSUAWQIJMNPRVBUSBMNEWQWQUTVCVAVDZAWNBMAMOBMFUIZUPZWNBMUQAWSKURULZXBIKUJUH
      ULXCQTUBXBIKMOPUDVBUSBMOFXBXBUTVCVAVDZWLWMWNUMZCUKZNULZDUKZOULZUMZGWOULZV
      EZDVFCVFZXFWPWLWMWNXAXEVGZAXNWKAXLDOUQCNUQZXNANOVHZWOWIUPZXPAXRJKVIUHZUNZ
      WOWIUPZAXSURULZLURULWIXSLUJUHULYAAWTXDYBSTJKVJVKUCUEWIXSLXTWOXTUTWOUTVBUS
      AXQXTWOWIAWTXDXQXTUOSTJKNORUDVLVKVMVPCDNOGWOWIWIUTZVNVAXLCDNOVOVQVRXOXMXF
      WPVECDEFNOXGEUOZXIFUOZUMZXKXFXLWPYDXHWMYEXJWNXGENVSXIFOVSVTYFGHWOUFWAWBWC
      WDCDEFNOGHWIWOUFYCWFUSWEABEFWIIJKLMNPQRSTUAUBUCUEWGWH $.
      $( [12-Jun-2014] $)
  $}

  ${
    $d t u v w z A $.  $d t u v w B $.  $d t u v C $.  $d w z D $.  $d v z J $.
    $d w x y z F $.  $d v w x y z N $.  $d v x y z ph $.  $d t u v w x y z X $.
    $d v x y z L $.  $d v x y z M $.  $d v w x y z W $.  $d t u v w x y z Y $.
    $d v z K $.  $d x y z P $.  $d t u v w x y z Z $.
    cnmpt21.x $e |- X = U. J $.
    cnmpt21.y $e |- Y = U. K $.
    cnmpt21.j $e |- ( ph -> J e. Top ) $.
    cnmpt21.k $e |- ( ph -> K e. Top ) $.
    $( The projection onto the first coordinate is continuous.  (Contributed by
       Mario Carneiro, 6-May-2014.) $)
    cnmpt1st $p |- ( ph ->
                     ( x e. X , y e. Y |-> x ) e. ( ( J tX K ) Cn J ) ) $=
      ( vz cv c1st co cfv cmpt wfn cvv wcel cmpt2 cxp cres ctx ccn wceq wss wfo
      fo1st fofn ax-mp ssv fnssres mp2an dffn5v mpbi fvres mpteq2ia fveq2 op1st
      cop vex syl6eq mpt2mpt 3eqtri ctop eqid tx1cn syl2anc syl5eqelr ) ABCFGBM
      ZUAZNFGUBZUCZDEUDODUEOZVNLVMLMZVNPZQZLVMVPNPZQVLVNVMRZVNVRUFNSRZVMSUGVTSS
      NUHWAUISSNUJUKVMULSVMNUMUNLVMVNUOUPLVMVQVSVPVMNUQURBCLFGVSVKVPVKCMZVAZUFV
      SWCNPVKVPWCNUSVKWBBVBUTVCVDVEADVFTEVFTVNVOTJKDEFGVMHIVMVGVHVIVJ $.
      $( [6-May-2014] $)

    $( The projection onto the second coordinate is continuous.  (Contributed
       by Mario Carneiro, 6-May-2014.) $)
    cnmpt2nd $p |- ( ph ->
                     ( x e. X , y e. Y |-> y ) e. ( ( J tX K ) Cn K ) ) $=
      ( vz cv c2nd co cfv cmpt wfn cvv wcel cmpt2 cxp cres ctx ccn wceq wss wfo
      fo2nd fofn ax-mp ssv fnssres mp2an dffn5v mpbi fvres mpteq2ia fveq2 op2nd
      cop vex syl6eq mpt2mpt 3eqtri ctop eqid tx2cn syl2anc syl5eqelr ) ABCFGCM
      ZUAZNFGUBZUCZDEUDOEUEOZVNLVMLMZVNPZQZLVMVPNPZQVLVNVMRZVNVRUFNSRZVMSUGVTSS
      NUHWAUISSNUJUKVMULSVMNUMUNLVMVNUOUPLVMVQVSVPVMNUQURBCLFGVSVKVPBMZVKVAZUFV
      SWCNPVKVPWCNUSWBVKBVBCVBUTVCVDVEADVFTEVFTVNVOTJKDEFGVMHIVMVGVHVIVJ $.
      $( [6-May-2014] $)

    cnmpt21.l $e |- ( ph -> L e. Top ) $.
    ${
      cnmpt2c.z $e |- Z = U. L $.
      cnmpt2c.p $e |- ( ph -> P e. Z ) $.
      $( A constant function is continuous.  (Contributed by Mario Carneiro,
         5-May-2014.) $)
      cnmpt2c $p |- ( ph -> ( x e. X , y e. Y |-> P ) e.
                            ( ( J tX K ) Cn L ) ) $=
        ( vz cv wceq cmpt2 ctx co cuni cmpt ccn cxp cop eqidd mpt2mpt ctop wcel
        txuni syl2anc mpteq1 syl syl5eqr eqid txtop cnmptc eqeltrd ) ABCHIDUAZR
        EFUBUCZUDZDUEZVCGUFUCAVBRHIUGZDUEZVEBCRHIDDRSBSCSUHTDUIUJAVFVDTZVGVETAE
        UKULZFUKULZVHMNEFHIKLUMUNRVFVDDUOUPUQARDVCGVDJVDURAVIVJVCUKULMNEFUSUNPO
        QUTVA $.
        $( [5-May-2014] $)
    $}

    cnmpt21.m $e |- ( ph -> M e. Top ) $.
    cnmpt21.a $e |- ( ph ->
                      ( x e. X , y e. Y |-> A ) e. ( ( J tX K ) Cn L ) ) $.
    ${
      $d x y B $.  $d w z C $.
      cnmpt21.z $e |- Z = U. L $.
      cnmpt21.b $e |- ( ph -> ( z e. Z |-> B ) e. ( L Cn M ) ) $.
      cnmpt21.c $e |- ( z = A -> B = C ) $.
      $( The composition of continuous functions is continuous.  (Contributed
         by Mario Carneiro, 5-May-2014.) $)
      cnmpt21 $p |- ( ph ->
                      ( x e. X , y e. Y |-> C ) e. ( ( J tX K ) Cn M ) ) $=
        ( vw vu vv vt cmpt cmpt2 ccom ctx co ccn wceq cv cfv cxp wral cop df-ov
        wcel wa simprl simprr wi cuni ctop txtop syl2anc eqid cnf syl3anc txuni
        wf feq2d mpbird fmpt2 sylibr ra42 syl imp ovmpt4g syl5eqr fveq2d adantr
        fmpt eleq1d rcla4v sylc fvmptg eqtrd wfun funmpt opelxpi adantl 3eqtr4d
        a1i fvco3 ralrimivva ax-17 hbmpt21 hbco hbfv hbeq hbral hbmpt22 eqeq12d
        opeq2 cbvral opeq1 ralbidv syl5bb sylib fveq2 ralxp wfn fco ffn eqfnfv2
        wb cnco syl32anc eqeltrrd ) ADNFUIZBCLMEUJZUKZBCLMGUJZHIULUMZKUNUMZAYGY
        HUOZUEUPZYGUQZYLYHUQZUOZUELMURZUSZAUFUPZUGUPZUTZYGUQZYTYHUQZUOZUGMUSZUF
        LUSZYQABUPZCUPZUTZYGUQZUUHYHUQZUOZCMUSZBLUSUUEAUUKBCLMAUUFLVBZUUGMVBZVC
        ZVCZUUHYFUQZYEUQZGUUIUUJUUPUUREYEUQZGUUPUUQEYEUUPUUQUUFUUGYFUMZEUUFUUGY
        FVAUUPUUMUUNENVBZUUTEUOAUUMUUNVDZAUUMUUNVEZAUUOUVAAUVACMUSBLUSZUUOUVAVF
        AYPNYFVOZUVDAUVEYIVGZNYFVOZAYIVHVBZJVHVBZYFYIJUNUMVBZUVGAHVHVBZIVHVBZUV
        HQRHIVIVJZSUAYFYIJUVFNUVFVKUBVLVMAYPUVFNYFAUVKUVLYPUVFUOQRHILMOPVNVJVPV
        QZBCLMENYFYFVKZVRVSUVABCLMVTWAWBZBCLMEYFNUVOWCVMWDWEUUPUVAGKVGZVBZUUSGU
        OUVPUUPUVAFUVQVBZDNUSZUVRUVPAUVTUUOANUVQYEVOZUVTAUVIKVHVBZYEJKUNUMVBZUW
        ASTUCYEJKNUVQUBUVQVKVLVMZDNUVQFYEYEVKZWGVSWFUVSUVRDENDUPEUOFGUVQUDWHWIW
        JZDEFGNUVQYEUDUWEWKVJWLUUPYEWMZUVEUUHYPVBZUUIUURUOUWGUUPDNFWNWRAUVEUUOU
        VNWFUUOUWHAUUFUUGLMWOWPYPNUUHYEYFWSVMUUPUUJUUFUUGYHUMZGUUFUUGYHVAUUPUUM
        UUNUVRUWIGUOUVBUVCUWFBCLMGYHUVQYHVKZWCVMWDWQWTUULUUDBUFLUULUFXAUUCBUGMY
        SMVBBXABUHUHUUAUUBBUHYTYGBUHYEYFUHUPZYEVBZBXABCUHLMEXBXCUWKYTVBBXAZXDBU
        HYTYHBCUHLMGXBUWMXDXEXFUULUUFYSUTZYGUQZUWNYHUQZUOZUGMUSUUFYRUOZUUDUUKUW
        QCUGMUUKUGXACUHUHUWOUWPCUHUWNYGCUHYEYFUWLCXABCUHLMEXGXCUWKUWNVBCXAZXDCU
        HUWNYHBCUHLMGXGUWSXDXEUUGYSUOZUUIUWOUUJUWPUWTUUHUWNYGUUGYSUUFXIZWEUWTUU
        HUWNYHUXAWEXHXJUWRUWQUUCUGMUWRUWOUUAUWPUUBUWRUWNYTYGUUFYRYSXKZWEUWRUWNY
        TYHUXBWEXHXLXMXJXNYOUUCUEUFUGLMYLYTUOYMUUAYNUUBYLYTYGXOYLYTYHXOXHXPVSAY
        GYPXQZYHYPXQZYKYQYAAYPUVQYGVOZUXCAUWAUVEUXEUWDUVNYPNUVQYEYFXRVJYPUVQYGX
        SWAAYPUVQYHVOZUXDAUVRCMUSBLUSUXFAUVRBCLMUWFWTBCLMGUVQYHUWJVRXNYPUVQYHXS
        WAUEYPYGYHXTVJVQAUVHUVIUWBUVJUWCYGYJVBUVMSTUAUCYFYEYIJKYBYCYD $.
        $( [5-May-2014] $)
    $}

    ${
      $d x y B $.  $d w z C $.
      cnmpt21f.f $e |- ( ph -> F e. ( L Cn M ) ) $.
      $( The composition of continuous functions is continuous.  (Contributed
         by Mario Carneiro, 5-May-2014.) $)
      cnmpt21f $p |- ( ph ->
                  ( x e. X , y e. Y |-> ( F ` A ) ) e. ( ( J tX K ) Cn M ) ) $=
        ( vz cv cfv cuni eqid cmpt ccn co wfn wceq wf ctop wcel cnf syl3anc ffn
        syl dffn5v sylib eqeltrrd fveq2 cnmpt21 ) ABCTDTUAZEUBZDEUBFGHIJKHUCZLM
        NOPQRVDUDZAETVDVCUEZHIUFUGZAEVDUHZEVFUIAVDIUCZEUJZVHAHUKULIUKULEVGULVJP
        QSEHIVDVIVEVIUDUMUNVDVIEUOUPTVDEUQURSUSVBDEUTVA $.
        $( [5-May-2014] $)
    $}

    $d z B $.  $d x y C $.
    cnmpt2t.b $e |- ( ph ->
                      ( x e. X , y e. Y |-> B ) e. ( ( J tX K ) Cn M ) ) $.
    $( The composition of continuous functions is continuous.  (Contributed by
       Mario Carneiro, 5-May-2014.) $)
    cnmpt2t $p |- ( ph ->
       ( x e. X , y e. Y |-> <. A , B >. ) e. ( ( J tX K ) Cn ( L tX M ) ) ) $=
      ( wcel vz vu vv vt ctx co cuni cmpt2 cfv cop cmpt ccn cxp wceq ctop txuni
      syl2anc mpteq1 syl fveq2 df-ov syl6eqr opeq12d mpt2mpt ax-17 hbmpt21 hbov
      cv hbop hbmpt22 wa oveq12 cbvmpt2 eqtri simp2 simp3 wral wi wf txtop eqid
      w3a cnf syl3anc feq2d mpbird sylibr ra42 3impib ovmpt4g mpt2eq3dva syl5eq
      fmpt2 eqtr3d txcnmpt syl32anc eqeltrrd ) AUAFGUEUFZUGZUAVHZBCJKDUHZUIZWTB
      CJKEUHZUIZUJZUKZBCJKDEUJZUHZWRHIUEUFULUFZAUAJKUMZXEUKZXFXHAXJWSUNZXKXFUNA
      FUOTZGUOTZXLNOFGJKLMUPUQZUAXJWSXEURUSAXKBCJKBVHZCVHZXAUFZXPXQXCUFZUJZUHZX
      HXKUBUCJKUBVHZUCVHZXAUFZYBYCXCUFZUJZUHYAUBUCUAJKXEYFWTYBYCUJZUNZXBYDXDYEY
      HXBYGXAUIYDWTYGXAUTYBYCXAVAVBYHXDYGXCUIYEWTYGXCUTYBYCXCVAVBVCVDUBUCBCUDJK
      YFXTBUDYDYEBUDYBYCXAUDVHZYBTZBVEZBCUDJKDVFYIYCTZBVEZVGBUDYBYCXCYKBCUDJKEV
      FYMVGVICUDYDYECUDYBYCXAYJCVEZBCUDJKDVJYLCVEZVGCUDYBYCXCYNBCUDJKEVJYOVGVIY
      IXTTZUBVEYPUCVEYBXPUNYCXQUNVKYDXRYEXSYBXPYCXQXAVLYBXPYCXQXCVLVCVMVNABCJKX
      TXGAXPJTZXQKTZWBZXRDXSEYSYQYRDHUGZTZXRDUNAYQYRVOZAYQYRVPZAYQYRUUAAUUACKVQ
      BJVQZYQYRVKZUUAVRAXJYTXAVSZUUDAUUFWSYTXAVSZAWRUOTZHUOTZXAWRHULUFTZUUGAXMX
      NUUHNOFGVTUQZPRXAWRHWSYTWSWAZYTWAWCWDAXJWSYTXAXOWEWFBCJKDYTXAXAWAZWMWGUUA
      BCJKWHUSWIBCJKDXAYTUUMWJWDYSYQYREIUGZTZXSEUNUUBUUCAYQYRUUOAUUOCKVQBJVQZUU
      EUUOVRAXJUUNXCVSZUUPAUUQWSUUNXCVSZAUUHIUOTZXCWRIULUFTZUURUUKQSXCWRIWSUUNU
      ULUUNWAWCWDAXJWSUUNXCXOWEWFBCJKEUUNXCXCWAZWMWGUUOBCJKWHUSWIBCJKEXCUUNUVAW
      JWDVCWKWLWNAUUIUUSUUHUUJUUTXFXITPQUUKRSUAHIWRXAXCXFWSUULXFWAWOWPWQ $.
      $( [5-May-2014] $)

    cnmpt22.z $e |- Z = U. L $.
    cnmpt22.w $e |- W = U. M $.
    cnmpt22.n $e |- ( ph -> N e. Top ) $.
    ${
      cnmpt22.c $e |- ( ph ->
                        ( z e. Z , w e. W |-> C ) e. ( ( L tX M ) Cn N ) ) $.
      cnmpt22.d $e |- ( ( z = A /\ w = B ) -> C = D ) $.
      $( The composition of continuous functions is continuous.  (Contributed
         by Mario Carneiro, 5-May-2014.) $)
      cnmpt22 $p |- ( ph ->
                      ( x e. X , y e. Y |-> D ) e. ( ( J tX K ) Cn N ) ) $=
        ( cop cmpt2 cfv ctx co ccn cv wcel w3a df-ov cuni wceq wral wa cxp ctop
        wi wf txtop syl2anc eqid cnf syl3anc txuni feq2d mpbird sylibr ra42 syl
        fmpt2 3impib wal jca r2al sylib 3ad2ant1 eleq1 bi2anan9 imbi12d cla42gv
        eleq1d syl3c ovmpt2ga syl5eqr mpt2eq3dva cnmpt2t cnmpt21f eqeltrrd ) AB
        CPQFGULZDEROHUMZUNZUMBCPQIUMJKUOUPZNUQUPABCPQXBIABURPUSZCURQUSZUTZXBFGX
        AUPZIFGXAVAXFFRUSZGOUSZINVBZUSZXGIVCAXDXEXHAXHCQVDBPVDZXDXEVEZXHVHAPQVF
        ZRBCPQFUMZVIZXLAXPXCVBZRXOVIZAXCVGUSZLVGUSZXOXCLUQUPUSXRAJVGUSZKVGUSZXS
        UAUBJKVJVKZUCUEXOXCLXQRXQVLZUGVMVNAXNXQRXOAYAYBXNXQVCUAUBJKPQSTVOVKZVPV
        QBCPQFRXOXOVLWAVRXHBCPQVSVTWBZAXDXEXIAXICQVDBPVDZXMXIVHAXNOBCPQGUMZVIZY
        GAYIXQOYHVIZAXSMVGUSZYHXCMUQUPUSYJYCUDUFYHXCMXQOYDUHVMVNAXNXQOYHYEVPVQB
        CPQGOYHYHVLWAVRXIBCPQVSVTWBZXFXHXIVEZDURZRUSZEURZOUSZVEZHXJUSZVHZEWCDWC
        ZYMXKXFXHXIYFYLWDZAXDUUAXEAYSEOVDDRVDZUUAAROVFZXJXAVIZUUCAUUELMUOUPZVBZ
        XJXAVIZAUUFVGUSZNVGUSXAUUFNUQUPUSUUHAXTYKUUIUCUDLMVJVKZUIUJXAUUFNUUGXJU
        UGVLXJVLVMVNAUUDUUGXJXAAXTYKUUDUUGVCUCUDLMROUGUHVOVKVPVQDEROHXJXAXAVLZW
        AVRYSDEROWEWFWGUUBYTYMXKVHDEFGROYNFVCZYPGVCZVEZYRYMYSXKUULYOXHUUMYQXIYN
        FRWHYPGOWHWIUUNHIXJUKWLWJWKWMDEFGROHIXAXJUKUUKWNVNWOWPABCWTXAJKUUFNPQST
        UAUBUUJUIABCFGJKLMPQSTUAUBUCUDUEUFWQUJWRWS $.
        $( [5-May-2014] $)
    $}

    cnmpt22f.f $e |- ( ph -> F e. ( ( L tX M ) Cn N ) ) $.
    $( The composition of continuous functions is continuous.  (Contributed by
       Mario Carneiro, 5-May-2014.) $)
    cnmpt22f $p |- ( ph -> ( x e. X , y e. Y |-> ( A F B ) ) e.
                           ( ( J tX K ) Cn N ) ) $=
      ( vz vw cv co cmpt2 ctx ccn cxp wceq cuni wf ctop wcel txtop syl2anc eqid
      wfn cnf syl3anc ffn syl txuni fneq2d mpbird fnov2 eqeltrrd oveq12 cnmpt22
      sylib ) ABCUHUIDEUHUJZUIUJZFUKZDEFUKGHIJKLMNOPQRSTUAUBUCUDUEUFAFUHUIOLVSU
      LZIJUMUKZKUNUKZAFOLUOZVDZFVTUPAWDFWAUQZVDZAWEKUQZFURZWFAWAUSUTZKUSUTFWBUT
      WHAIUSUTZJUSUTZWITUAIJVAVBUFUGFWAKWEWGWEVCWGVCVEVFWEWGFVGVHAWCWEFAWJWKWCW
      EUPTUAIJOLUDUEVIVBVJVKUHUIOLFVLVPUGVMVQDVREFVNVO $.
      $( [5-May-2014] $)
  $}

  ${
    $d x y W $.  $d x y X $.  $d x y Y $.  $d x y Z $.
    cnmpt1res.1 $e |- X = U. J $.
    cnmpt1res.2 $e |- K = ( subSp ` <. Y , J >. ) $.
    cnmpt1res.3 $e |- ( ph -> J e. Top ) $.
    cnmpt1res.4 $e |- ( ph -> L e. Top ) $.
    cnmpt1res.5 $e |- ( ph -> Y C_ X ) $.
    ${
      cnmpt1res.6 $e |- ( ph -> ( x e. X |-> A ) e. ( J Cn L ) ) $.
      $( The restriction of a continuous function to a subset is continuous.
         (Contributed by Mario Carneiro, 5-Jun-2014.) $)
      cnmpt1res $p |- ( ph -> ( x e. Y |-> A ) e. ( K Cn L ) ) $=
        ( cmpt cres ccn co ctop wcel wss wceq resmpt syl cop csubsp cfv cnsubsp
        syl22anc oveq1i syl6eleqr eqeltrrd ) ABGCOZHPZBHCOZEFQRZAHGUAZUNUOUBMBG
        HCUCUDAUNHDUEUFUGZFQRZUPADSTFSTUQUMDFQRTUNUSTKLMNHUMDFGIUHUIEURFQJUJUKU
        L $.
        $( [5-Jun-2014] $)
    $}

    cnmpt2res.6 $e |- Z = U. M $.
    cnmpt2res.7 $e |- N = ( subSp ` <. W , M >. ) $.
    cnmpt2res.8 $e |- ( ph -> M e. Top ) $.
    cnmpt2res.9 $e |- ( ph -> W C_ Z ) $.
    cnmpt2res.10 $e |- ( ph -> ( x e. X , y e. Z |-> A ) e.
                               ( ( J tX M ) Cn L ) ) $.
    $( The restriction of a continuous function to a subset is continuous.
       (Contributed by Mario Carneiro, 6-Jun-2014.) $)
    cnmpt2res $p |- ( ph -> ( x e. Y , y e. W |-> A ) e.
                            ( ( K tX N ) Cn L ) ) $=
      ( cmpt2 cxp cres ctx co ccn wss wceq resmpt2 syl2anc cop csubsp ctop wcel
      cfv cuni txtop xpss12 txuni sseqtrd cnsubsp syl22anc cvv topopn syl ssexg
      eqid txsubsp oveq12i syl6eqr oveq1d eleqtrd eqeltrrd ) ABCKMDUDZLJUEZUFZB
      CLJDUDZFIUGUHZGUIUHZALKUJZJMUJZVSVTUKRUBBCKMLJDULUMAVSVREHUGUHZUNUOURZGUI
      UHZWBAWEUPUQZGUPUQVRWEUSZUJVQWEGUIUHUQVSWGUQAEUPUQZHUPUQZWHPUAEHUTUMQAVRK
      MUEZWIAWCWDVRWLUJRUBLKJMVAUMAWJWKWLWIUKPUAEHKMNSVBUMVCUCVRVQWEGWIWIVJVDVE
      AWFWAGUIAWFLEUNUOURZJHUNUOURZUGUHZWAAWJWKLVFUQZJVFUQZWFWOUKPUAAWCKEUQZWPR
      AWJWRPEKNVGVHLKEVIUMAWDMHUQZWQUBAWKWSUAHMSVGVHJMHVIUMLJEHVFVFVKVEFWMIWNUG
      OTVLVMVNVOVP $.
      $( [6-Jun-2014] $)
  $}

  ${
    $d t w z A $.  $d w z J $.  $d w z K $.  $d w x y z L $.  $d t w x y z X $.
    $d w x y z ph $.  $d t w x y z Y $.
    cnmptcom.1 $e |- X = U. J $.
    cnmptcom.2 $e |- Y = U. K $.
    cnmptcom.3 $e |- ( ph -> J e. Top ) $.
    cnmptcom.4 $e |- ( ph -> K e. Top ) $.
    cnmptcom.5 $e |- ( ph -> L e. Top ) $.
    cnmptcom.6 $e |- ( ph ->
                       ( x e. X , y e. Y |-> A ) e. ( ( J tX K ) Cn L ) ) $.
    $( The argument converse of a continuous function is continuous.
       (Contributed by Mario Carneiro, 6-Jun-2014.) $)
    cnmptcom $p |- ( ph ->
                     ( y e. Y , x e. X |-> A ) e. ( ( K tX J ) Cn L ) ) $=
      ( vz vw vt co wcel cmpt2 cv ctx ccn cxp wceq cuni wral ctop txtop syl2anc
      wfn eqid cnf syl3anc txuni feq2d mpbird fmpt2 ralcom bitr3i sylib ffn syl
      wf fnov2 wa wi ax-17 hbmpt22 hbov hbmpt21 hbeq oveq2 oveq1 eqeq12d imbi2d
      hbim ra42 com12 w3a 3com12 eqtr4d 3expia syld vtocl2gaf 3impib mpt2eq3dva
      ovmpt4g cnmpt2nd cnmpt1st cnmpt22f eqeltrd ) ACBIHDUAZPQIHQUBZPUBZBCHIDUA
      ZSZUAZFEUCSGUDSAWNPQIHWPWOWNSZUAZWSAWNIHUEZULZWNXAUFAXBGUGZWNVEZXCADXDTZB
      HUHCIUHZXEAHIUEZXDWQVEZXGAXIEFUCSZUGZXDWQVEZAXJUITZGUITWQXJGUDSTXLAEUITZF
      UITZXMLMEFUJUKNOWQXJGXKXDXKUMXDUMUNUOAXHXKXDWQAXNXOXHXKUFLMEFHIJKUPUKUQUR
      XIXFCIUHBHUHXGBCHIDXDWQWQUMZUSXFBCHIUTVAVBZCBIHDXDWNWNUMZUSVBXBXDWNVCVDPQ
      IHWNVFVBAPQIHWRWTAWPITZWOHTZWRWTUFZXSXTVGAYAABUBZCUBZWQSZYCYBWNSZUFZVHAYB
      WPWQSZWPYBWNSZUFZVHAYAVHCBRWPWOIHRUBZWPTZCVIZYKBVIZYJWOTBVIZAYICACVICRRYG
      YHCRYBWPWQYJYBTCVIZBCRHIDVJYLVKCRWPYBWNYLCBRIHDVLYOVKVMVRAYABABVIBRRWRWTB
      RWOWPWQYNBCRHIDVLYMVKBRWPWOWNYMCBRIHDVJYNVKVMVRYCWPUFZYFYIAYPYDYGYEYHYCWP
      YBWQVNYCWPYBWNVOVPVQYBWOUFZYIYAAYQYGWRYHWTYBWOWPWQVOYBWOWPWNVNVPVQYCITZYB
      HTZVGZAXFYFAYTXFAXGYTXFVHXQXFCBIHVSVDVTYRYSXFYFYRYSXFWAYDDYEYSYRXFYDDUFBC
      HIDWQXDXPWIWBCBIHDWNXDXRWIWCWDWEWFVTWGWHWCAPQWOWPWQFEEFGIIHHKJMLLMAPQFEIH
      KJMLWJAPQFEIHKJMLWKJKNOWLWM $.
      $( [6-Jun-2014] $)
  $}


