$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      TopBases for topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z B $.
    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasisg $p |- ( B e. C -> ( B e. TopBases <->
          A. x e. B A. y e. B ( x i^i y ) C_ U. ( B i^i ~P ( x i^i y ) ) ) ) $=
      ( vz cv cin cpw cuni wss wral ctb ineq1 unieqd sseq2d raleqbi1dv df-bases
      wceq elab2g ) AFBFGZEFZTHZGZIZJZBUAKZAUAKTCUBGZIZJZBCKZACKECLDUFUJAUACUEU
      IBUACUACRZUDUHTUKUCUGUACUBMNOPPEABQS $.
      $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasis2g $p |- ( B e. C -> ( B e. TopBases <->
           A. x e. B A. y e. B A. z e. ( x i^i y )
              E. w e. B ( z e. w /\ w C_ ( x i^i y ) ) ) ) $=
      ( wcel ctb cv cin cpw cuni wss wral wa wrex isbasisg wex anbi2i bitri
      dfss3 elin df-pw abeq2i exbii eluni df-rex 3bitr4i ralbii 2ralbii syl6bb
      an12 ) EFGEHGAIBIJZEUMKZJZLZMZBENAENCIZDIZGZUSUMMZOZDEPZCUMNZBENAENABEFQU
      QVDABEEUQURUPGZCUMNVDCUMUPUAVEVCCUMUTUSUOGZOZDRUSEGZVBOZDRVEVCVGVIDVGUTVH
      VAOZOVIVFVJUTVFVHUSUNGZOVJUSEUNUBVKVAVHVADUNDUMUCUDSTSUTVHVAULTUEDURUOUFV
      BDEUGUHUITUJUK $.
      $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology."  Definition of
       basis in [Munkres] p. 78. $)
    isbasis3g $p |- ( B e. C -> ( B e. TopBases <-> ( A. x e. B x C_ U. B /\
     A. x e. U. B E. y e. B x e. y /\ A. x e. B A. y e. B A. z e. ( x i^i y )
     E. w e. B ( z e. w /\ w C_ ( x i^i y ) ) ) ) ) $=
      ( wcel ctb cv cin wss wa wrex wral cuni w3a isbasis2g elssuni rgen eluni2
      biimpi pm3.2i biantrur df-3an bitr4i syl6bb ) EFGEHGCIDIZGUGAIZBIZJZKLDEM
      CUJNBENAENZUHEOZKZAENZUHUIGBEMZAULNZUKPZABCDEFQUKUNUPLZUKLUQURUKUNUPUMAEU
      HERSUOAULUHULGUOBUHETUASUBUCUNUPUKUDUEUF $.
      $( [17-Jul-2006] $)
  $}

  ${
    $d w x A $.  $d w x y z B $.  $d w x y z C $.  $d w x y z D $.
    $( Property of a basis. $)
    basis1 $p |- ( ( B e. TopBases /\ C e. B /\ D e. B ) ->
                    ( C i^i D ) C_ U. ( B i^i ~P ( C i^i D ) ) ) $=
      ( vx vy ctb wcel cin cpw cuni wss cv wral wa isbasisg pweqd ineq2d unieqd
      wceq sseq12d ibi ineq1 ineq2 rcla42v syl5com 3impib ) AFGZBAGZCAGZBCHZAUJ
      IZHZJZKZUGDLZELZHZAUQIZHZJZKZEAMDAMZUHUINUNUGVBDEAFOUAVAUNBUPHZAVCIZHZJZK
      DEBCAAUOBSZUQVCUTVFUOBUPUBZVGUSVEVGURVDAVGUQVCVHPQRTUPCSZVCUJVFUMUPCBUCZV
      IVEULVIVDUKAVIVCUJVJPQRTUDUEUF $.
      $( [16-Jul-2006] $)

    $( Property of a basis. $)
    basis2 $p |- ( ( ( B e. TopBases /\ C e. B ) /\ ( D e. B /\
          A e. ( C i^i D ) ) ) -> E. x e. B ( A e. x /\ x C_ ( C i^i D ) ) ) $=
      ( vw vy vz ctb wcel cin cv wss wa wrex wi wral wceq rexbidv syl isbasis2g
      ibi wb ineq1 sseq2 anbi2d raleqbi1dv ineq2 rcla42v anbi1d rcla4cv syl6com
      eleq1 exp3a imp43 ) CIJZDCJZECJZBDEKZJZBALZJZVAUSMZNZACOZUPUQURUTVEPZUPFL
      ZVAJZVAGLZHLZKZMZNZACOZFVKQZHCQGCQZUQURNZVFPUPVPGHFACIUAUBVQVPVHVCNZACOZF
      USQZVFVOVTVHVADVJKZMZNZACOZFWAQZGHDECCVIDRVKWARZVOWEUCVIDVJUDVNWDFVKWAWFV
      MWCACWFVLWBVHVKWAVAUEUFSUGTVJERWAUSRZWEVTUCVJEDUHWDVSFWAUSWGWCVRACWGWBVCV
      HWAUSVAUEUFSUGTUIVSVEFBUSVGBRZVRVDACWHVHVBVCVGBVAUMUJSUKULTUNUO $.
      $( [17-Jul-2006] $)
  $}

  ${
    $d B w x y z $.  $d C w x y z $.
    $( If a set is closed under finite intersection, then it is a basis for a
       topology.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    fiinbas $p |- ( ( B e. C /\ A. x e. B A. y e. B ( x i^i y ) e. B )
                       -> B e. TopBases ) $=
      ( vz vw wcel cv cin wral ctb wss wa wrex wi ssid wceq eleq2 sseq1 ralimdv
      anbi12d rcla4ev mpanr2 ralrimiva a1i isbasis2g sylibrd imp ) CDGZAHBHIZCG
      ZBCJZACJZCKGZUIUMEHZFHZGZUPUJLZMZFCNZEUJJZBCJZACJUNUIULVBACUIUKVABCUKVAOU
      IUKUTEUJUKUOUJGZUJUJLZUTUJPUSVCVDMFUJCUPUJQUQVCURVDUPUJUORUPUJUJSUAUBUCUD
      UETTABEFCDUFUGUH $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d x y z A $.  $d t u v w x y z B $.  $d w x y z J $.  $d x y z V $.
    $d x y C $.
    $( The topology generated by a basis.  (Revised by Mario Carneiro,
       10-Jan-2015.) $)
    tgval $p |- ( B e. V -> ( topGen ` B ) =
                  { x | x C_ U. ( B i^i ~P x ) } ) $=
      ( vy wcel cvv cv cpw cin cuni wss cab ctg cfv wceq elex wa uniexg abssexg
      uniin sstr mpan2 ssin sylibr ss2abi ssexg mpan ineq1 unieqd sseq2d abbidv
      3syl df-topgen fvmptg syl2anc ) BCEZBFEAGZBUQHZIZJZKZALZFEZBMNVBOBCPUPBJZ
      FEUQVDKUQURJZKZQZALZFEZVCBCRVFAVDFSVBVHKVIVCVAVGAVAUQVDVEIZKZVGVAUTVJKVKB
      URTUQUTVJUAUBUQVDVEUCUDUEVBVHFUFUGULDBUQDGZURIZJZKZALVBFFMVLBOZVOVAAVPVNU
      TUQVPVMUSVLBURUHUIUJUKDAUMUNUO $.
      $( [10-Jan-2015] $) $( [16-Jul-2006] $)

    $( Definition of a topology generated by a basis in [Munkres] p. 78.  Later
       we show (in ~ tgcl ) that ` ( topGen `` B ) ` is indeed a topology (on
       ` U. B ` ; see ~ unitg ). $)
    tgval2 $p |- ( B e. V -> ( topGen ` B ) =
         { x | ( x C_ U. B /\ A. y e. x E. z e. B ( y e. z /\ z C_ x ) ) } ) $=
      ( wcel ctg cfv cv cpw cuni wss cab wa wrex wral dfss3 ralbii wex anbi2i
      cin tgval inss1 uniss ax-mp sseli pm4.71ri r19.26 3bitri elin bitri exbii
      an12 eluni df-rex 3bitr4i elpw rexbii bitr2i anbi12i bitr4i abbii syl6eq
      vex ) DEFDGHAIZDVEJZUAZKZLZAMVEDKZLZBIZCIZFZVMVELZNZCDOZBVEPZNZAMADEUBVIV
      SAVIVLVJFZBVEPZVLVHFZBVEPZNZVSVIWCVTWBNZBVEPWDBVEVHQWBWEBVEWBVTVHVJVLVGDL
      VHVJLDVFUCVGDUDUEUFUGRVTWBBVEUHUIVKWAVRWCBVEVJQVQWBBVEWBVNVMVFFZNZCDOZVQV
      NVMVGFZNZCSVMDFZWGNZCSWBWHWJWLCWJVNWKWFNZNWLWIWMVNVMDVFUJTVNWKWFUMUKULCVL
      VGUNWGCDUOUPWGVPCDWFVOVNVMVECVDUQTURUSRUTVAVBVC $.
      $( [10-Jan-2015] $) $( [15-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg $p |- ( B e. V -> ( A e. ( topGen ` B ) <->
           A C_ U. ( B i^i ~P A ) ) ) $=
      ( vx wcel ctg cfv cv cpw cin cuni wss cab tgval eleq2d elex adantl inex1g
      cvv uniexg syl ssexg sylan2 ancoms wceq id ineq2d unieqd sseq12d pm5.21nd
      pweq elabg bitrd ) BCEZABFGZEADHZBUPIZJZKZLZDMZEZABAIZJZKZLZUNUOVAADBCNOU
      NVBVFASEZVBVGUNAVAPQVFUNVGUNVFVESEZVGUNVDSEVHBVCCRVDSTUAAVESUBUCUDUTVFDAS
      UPAUEZUPAUSVEVIUFVIURVDVIUQVCBUPAUKUGUHUIULUJUM $.
      $( [10-Jan-2015] $) $( [16-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg2 $p |- ( B e. V -> ( A e. ( topGen ` B ) <->
           ( A C_ U. B /\ A. x e. A E. y e. B ( x e. y /\ y C_ A ) ) ) ) $=
      ( vz wcel ctg cfv cv cuni wss wa wrex wral cab tgval2 eleq2d cvv elex
      adantl uniexg ssexg sylan2 ancoms adantrr wceq sseq1 sseq2 anbi2d rexbidv
      raleqbi1dv anbi12d elabg pm5.21nd bitrd ) DEGZCDHIZGCFJZDKZLZAJBJZGZVBUSL
      ZMZBDNZAUSOZMZFPZGZCUTLZVCVBCLZMZBDNZACOZMZUQURVICFABDEQRUQVJVPCSGZVJVQUQ
      CVITUAUQVKVQVOVKUQVQUQVKUTSGVQDEUBCUTSUCUDUEUFVHVPFCSUSCUGZVAVKVGVOUSCUTU
      HVFVNAUSCVRVEVMBDVRVDVLVCUSCVBUIUJUKULUMUNUOUP $.
      $( [10-Jan-2015] $) $( [15-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg2b $p |- ( B e. V -> ( A e. ( topGen ` B ) <->
                   A. x e. A E. y e. B ( x e. y /\ y C_ A ) ) ) $=
      ( wcel ctg cfv cuni wss cv wa wrex wral eltg2 reximi eluni2 sylibr ralimi
      simpl dfss3 pm4.71ri syl6bbr ) DEFCDGHFCDIZJZAKZBKZFZUGCJZLZBDMZACNZLULAB
      CDEOULUEULUFUDFZACNUEUKUMACUKUHBDMUMUJUHBDUHUITPBUFDQRSACUDUARUBUC $.
      $( [10-Jan-2015] $) $( [17-Jun-2014] $)

    $( Property of a member of a topology generated by a basis. $)
    tg1 $p |- ( A e. ( topGen ` B ) -> A C_ U. B ) $=
      ( vx vy ctg cdm wcel cfv cuni wss elfvdm cv wa wrex wral simprbda mpancom
      eltg2 ) BEFZGZABEHGZABIJZABEKTUAUBCLDLZGUCAJMDBNCAOCDABSRPQ $.
      $( [20-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg2 $p |- ( ( A e. ( topGen ` B ) /\ C e. A ) ->
               E. x e. B ( C e. x /\ x C_ A ) ) $=
      ( vy ctg cfv wcel cv wss wa wrex cdm elfvdm wral eltg2b wceq eleq1 anbi1d
      wi rexbidv rcla4cv syl6bi mpcom imp ) BCFGHZDBHZDAIZHZUHBJZKZACLZCFMZHZUF
      UGULTZBCFNUNUFEIZUHHZUJKZACLZEBOUOEABCUMPUSULEDBUPDQZURUKACUTUQUIUJUPDUHR
      SUAUBUCUDUE $.
      $( [20-Jul-2006] $)

    $( A member of a basis is a subset of the topology it generates. $)
    bastg $p |- ( B e. V -> B C_ ( topGen ` B ) ) $=
      ( vx wcel ctg cfv cv cpw cin cuni wss wa simpr vex pwid a1i elin sylanbrc
      elssuni syl ex eltg sylibrd ssrdv ) ABDZCAAEFZUECGZADZUGAUGHZIZJKZUGUFDUE
      UHUKUEUHLZUGUJDZUKULUHUGUIDZUMUEUHMUNULUGCNOPUGAUIQRUGUJSTUAUGABUBUCUD $.
      $( [10-Jan-2015] $) $( [16-Jul-2006] $)

    $( The topology generated by a basis ` B ` is a topology on ` U. B ` .
       Importantly, this theorem means that we don't have to specify separately
       the base set for the topological space generated by a basis.  In other
       words, any member of the class ` TopBases ` completely specifies the
       basis it corresponds to. $)
    unitg $p |- ( B e. V -> U. ( topGen ` B ) = U. B ) $=
      ( vx vy wcel ctg cfv cuni cv wrex eluni2 cpw cin wss wi inss1 uniss ax-mp
      eltg sseld sstr mpan2 syl6bi rexlimdv syl5bi bastg syl impbid eqrdv ) ABE
      ZCAFGZHZAHZUJCIZULEZUNUMEZUOUNDIZEZDUKJUJUPDUNUKKUJURUPDUKUJUQUKEUQAUQLZM
      ZHZNZURUPOUQABSVBUQUMUNVBVAUMNZUQUMNUTANVCAUSPUTAQRUQVAUMUAUBTUCUDUEUJUMU
      LUNUJAUKNUMULNABUFAUKQUGTUHUI $.
      $( [16-Jul-2006] $)

    $( Show that a basis generates a topology.  Remark in [Munkres] p. 79. $)
    tgcl $p |- ( B e. TopBases -> ( topGen ` B ) e. Top ) $=
      ( vu vv vx vy vt vz vw ctb wcel cv wss cuni wi wral wa wrex ra4 ex eltg2
      ctg cfv wal cin ctop uniss adantl wceq unitg adantr sseqtrd eluni2 eltg2b
      ssel2 syl6bi imp31 an32s sylan2 an42s elssuni syl5com anim2d ad2antrl mpd
      sstr2 reximdv exp32 rexlimdv syl5bi ralrimiv sylibrd alrimiv inss1 syl5ss
      jca tg1 simplbda syl im2anan9 elin reeanv 3imtr4g anandis biimpri anim12i
      ss2in an4s basis2 adantllr adantrrr ad2antll sylanr2 rexlimdva a2d syldan
      com12 imp ralrimivv cvv wb fvex istopg ax-mp sylanbrc ) AIJZBKZAUAUBZLZXF
      MZXGJZNZBUCZXFCKZUDZXGJZCXGOBXGOZXGUEJZXEXKBXEXHXIAMZLZDKZEKZJZYAXILZPZEA
      QZDXIOZPZXJXEXHYGXEXHPZXSYFYHXIXGMZXRXHXIYILXEXFXGUFUGXEYIXRUHXHAIUIUJUKY
      HYEDXIXTXIJXTFKZJZFXFQYHYEFXTXFULYHYKYEFXFYHYJXFJZYKYEYHYLYKPPYBYAYJLZPZE
      AQZYEXEYKXHYLYOXHYLPXEYKPYJXGJZYOXFXGYJUNXEYPYKYOXEYPYKYOXEYPYODYJOYKYOND
      EYJAIUMYODYJRUOUPUQURUSYLYOYENYHYKYLYNYDEAYLYMYCYBYLYJXILYMYCYJXFUTYAYJXI
      VEVAVBVFVCVDVGVHVIVJVOSDEXIAITVKVLXEXOBCXGXGXEXFXGJZXMXGJZPZXNXRLZYKYJXNL
      ZPZFAQZDXNOZPZXOXEYSUUEXEYSPZYTUUDYQYTXEYRYQXNXFXRXFXMVMXFAVPVNVCUUFUUCDX
      NXEYSXTXNJZXTGKZJZUUHXFLZPZXTHKZJZUULXMLZPZPZHAQZGAQZNZUUGUUCNZXEYQYRUUSX
      EYQPZXEYRPZPXTXFJZXTXMJZPUUKGAQZUUOHAQZPUUGUURUVAUVCUVEUVBUVDUVFUVAUVEDXF
      OZUVCUVENXEYQXFXRLUVGDGXFAITVQUVEDXFRVRUVBUVFDXMOZUVDUVFNXEYRXMXRLUVHDHXM
      AITVQUVFDXMRVRVSXTXFXMVTUUKUUOGHAAWAWBWCXEUUSUUTXEUUGUURUUCXEUUGUURUUCNXE
      UUGPZUUQUUCGAUVIUUHAJZPZUUPUUCHAUVKUULAJZUUPUUCUUPUVKUVLXTUUHUULUDZJZUVMX
      NLZPZUUCUUIUUMUUJUUNUVPUUIUUMPZUVNUUJUUNPUVOUVNUVQXTUUHUULVTWDUUHXFUULXMW
      FWEWGUVKUVLUVPPPYKYJUVMLZPZFAQZUUCUVKUVLUVNUVTUVOXEUVJUVLUVNPUVTUUGFXTAUU
      HUULWHWIWJUVPUVTUUCNZUVKUVLUVOUWAUVNUVOUVSUUBFAUVOUVRUUAYKUVRUVOUUAYJUVMX
      NVEWPVBVFUGWKVDWLVGVHWMSWNWQWOVJVOSDFXNAITVKWRXGWSJXQXLXPPWTAUAXABCWSXGXB
      XCXD $.
      $( [17-Jul-2006] $)

    $( Alternate expression for the topology generated by a basis.  Lemma 2.1
       of [Munkres] p. 80. $)
    tgval3 $p |- ( B e. TopBases ->
               ( topGen ` B ) = { x | E. y ( y C_ B /\ x = U. y ) } ) $=
      ( vz vw ctb wcel cv wss cuni wceq wa wex wrex wi bitri adantrr cvv ax-17
      ex ctg cfv simpr abssi eltg2b biimpa elequ1 anbi1d rexbidv rcla4cv df-rex
      cab wral an12 ancom anbi2i syl6ib syl ssel impcom exlimiv impbid1 eluniab
      exbii syl6bbr eqrdv abssexg hbab1 hbss hbuni hbeq hban sseq1 unieq eqeq2d
      vex anbi12d cla4egf mp2b simprr ctop tgcl adantr bastg sstr sylan2 ancoms
      sylancr uniopn syl2anc eqeltrd exlimdv impbid abbi2dv ) CFGZBHZCIZAHZWPJZ
      KZLZBMZACUAUBZWOWRXCGZXBWOXDXBWOXDLZWPWRIZWPCGZLZBULZCIZWRXIJZKZXBXHBCXFX
      GUCUDXEDWRXKXEDHZWRGZXMWPGZXHLZBMZXMXKGXEXNXQXEEHZWPGZXFLZBCNZEWRUMZXNXQO
      WOXDYBEBWRCFUEUFYBXNXOXFLZBCNZXQYAYDEXMWRXRXMKZXTYCBCYEXSXOXFEDBUGUHUIUJY
      DXGYCLZBMXQYCBCUKYFXPBYFXOXGXFLZLXPXGXOXFUNYGXHXOXGXFUOUPPVDPUQURXPXNBXOX
      FXNXGXFXOXNWPWRXMUSUTQVAVBXHBXMVCVEVFWRRGXIRGXJXLLZXBOAVPXGBWRRVGXAYHBDXI
      RXHBDVHZXJXLBBDXICYIXMCGBSVIBDDWRXKXNBSBDXIYIVJVKVLWPXIKZWQXJWTXLWPXICVMY
      JWSXKWRWPXIVNVOVQVRVSWHTWOXAXDBWOXAXDWOXALWRWSXCWOWQWTVTWOWQWSXCGZWTWOWQL
      XCWAGZWPXCIZYKWOYLWQCWBWCWQWOYMWOWQCXCIYMCFWDWPCXCWEWFWGWPXCWIWJQWKTWLWMW
      N $.
      $( [17-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg3 $p |- ( B e. TopBases -> ( A e. ( topGen ` B ) <->
           E. x ( x C_ B /\ A = U. x ) ) ) $=
      ( vy ctb wcel ctg cfv cv wss cuni wceq wa wex cab tgval3 eleq2d cvv uniex
      vex eleq1 mpbiri adantl exlimiv eqeq1 anbi2d exbidv elab3 syl6bb ) CEFZBC
      GHZFBAIZCJZDIZULKZLZMZANZDOZFUMBUOLZMZANZUJUKUSBDACPQURVBDBVABRFZAUTVCUMU
      TVCUORFULATSBUORUAUBUCUDUNBLZUQVAAVDUPUTUMUNBUOUEUFUGUHUI $.
      $( [15-Jul-2006] $)

    $( A topology is its own basis. $)
    topbas $p |- ( J e. Top -> J e. TopBases ) $=
      ( vz vw vx vy ctop wcel ctb cv cin wss wrex wral inopn 3expb adantr simpr
      wa ssid jctir wceq eleq2 sseq1 anbi12d rcla4ev syl2anc ralrimdv ralrimivv
      exp31 isbasis2g mpbird ) AFGZAHGBIZCIZGZUNDIZEIZJZKZRZCALZBURMZEAMDAMULVB
      DEAAULUPAGZUQAGZRZVABURULVEUMURGZVAULVERZVFRZURAGZVFURURKZRZVAVGVIVFULVCV
      DVIUPUQANOPVHVFVJVGVFQURSTUTVKCURAUNURUAUOVFUSVJUNURUMUBUNURURUCUDUEUFUIU
      GUHDEBCAFUJUK $.
      $( [17-Jul-2006] $)

    $( A topology is its own basis. $)
    tgtop $p |- ( J e. Top -> ( topGen ` J ) = J ) $=
      ( vx vy ctop wcel ctg cfv cv wss cuni wceq wa wex ctb wb topbas eltg3 syl
      simpr uniopn adantr eqeltrd expl exlimdv sylbid ssrdv bastg eqssd ) ADEZA
      FGZAUIBUJAUIBHZUJEZCHZAIZUKUMJZKZLZCMZUKAEZUIANEZULUROAPZCUKAQRUIUQUSCUIU
      NUPUSUIUNLZUPLUKUOAVBUPSVBUOAEUPUMATUAUBUCUDUEUFUIUTAUJIVAANUGRUH $.
      $( [18-Jul-2006] $)

    $( Membership in a topology, expressed without quantifiers. $)
    eltop $p |- ( J e. Top -> ( A e. J <-> A C_ U. ( J i^i ~P A ) ) ) $=
      ( ctop wcel ctg cfv cpw cin cuni wss eleq2d ctb wb topbas eltg syl bitr3d
      tgtop ) BCDZABEFZDZABDABAGHIJZSTBABRKSBLDUAUBMBNABLOPQ $.
      $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop2 $p |- ( J e. Top -> ( A e. J <->
                   A. x e. A E. y e. J ( x e. y /\ y C_ A ) ) ) $=
      ( ctop wcel ctg cfv cv wss wa wrex wral tgtop eleq2d ctb wb topbas eltg2b
      syl bitr3d ) DEFZCDGHZFZCDFAIBIZFUECJKBDLACMZUBUCDCDNOUBDPFUDUFQDRABCDPST
      UA $.
      $( [23-Aug-2007] $) $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop3 $p |- ( J e. Top -> ( A e. J <-> E. x ( x C_ J /\ A = U. x ) ) ) $=
      ( ctop wcel ctg cfv cv wss cuni wceq wa wex tgtop eleq2d ctb topbas eltg3
      wb syl bitr3d ) CDEZBCFGZEZBCEAHZCIBUEJKLAMZUBUCCBCNOUBCPEUDUFSCQABCRTUA
      $.
      $( [29-Mar-2008] $) $( [19-Jul-2006] $)
    $( A collection of finite intersections is a basis.  The initial set is a
       subbasis for the topology.  (Contributed by Jeff Hankins,
       25-Aug-2009.) $)
    fibas $p |- ( fi ` A ) e. TopBases $=
      ( vx vy cfi cfv cvv wcel cv cin wral ctb fvex fiin rgen2a fiinbas mp2an )
      ADEZFGBHZCHZIQGZCQJBQJQKGADLTBCQRSAMNBCQFOP $.
      $( [24-Nov-2013] $) $( [25-Aug-2009] $)
  $}

  $( The topology generator function is idempotent. $)
  tgidm $p |- ( B e. TopBases ->
               ( topGen ` ( topGen ` B ) ) = ( topGen ` B ) ) $=
    ( ctb wcel ctg cfv ctop wceq tgcl tgtop syl ) ABCADEZFCKDEKGAHKIJ $.
    $( [18-Jul-2006] $)

  $( Two ways to express that a basis is a topology. $)
  bastop $p |- ( B e. TopBases -> ( B e. Top <-> ( topGen ` B ) = B ) ) $=
    ( ctb wcel ctop ctg cfv wceq tgtop wa simpr tgcl adantr eqeltrrd ex impbid2
    ) ABCZADCZAEFZAGZAHPSQPSIRADPSJPRDCSAKLMNO $.
    $( [14-Aug-2007] $) $( [18-Jul-2006] $)

  $( The topology generation function is one-to-one when applied to completed
     topologies. $)
  tgtop11 $p |- ( ( J e. Top /\ K e. Top /\ ( topGen ` J ) = ( topGen ` K ) )
                 -> J = K ) $=
    ( ctop wcel ctg cfv wceq w3a simp3 tgtop 3ad2ant1 3ad2ant2 3eqtr3d ) ACDZBC
    DZAEFZBEFZGZHPQABNORINOPAGRAJKONQBGRBJLM $.
    $( [11-Jun-2007] $) $( [18-Jul-2006] $)

  $( The singleton of the empty set is the only topology possible for an empty
     underlying set. $)
  0top $p |- ( J e. Top -> ( U. J = (/) <-> J = { (/) } ) ) $=
    ( ctop wcel c0 csn wceq wo cuni olc wn 0opn n0i syl pm2.21d idd impbid2 wss
    jaod uni0b sssn bitr2i syl6rbb ) ABCZADEZFZADFZUEGZAHDFZUCUEUGUEUFIUCUFUEUE
    UCUFUEUCDACUFJAKADLMNUCUEORPUHAUDQUGASADTUAUB $.
    $( [24-Mar-2008] $) $( [9-Sep-2006] $)

  ${
    $d u v w x y z B $.  $d u v w x y z C $.  $d v w x y z J $.  $d x V $.
    $( Subset relation for generated topologies. $)
    tgss $p |- ( ( C e. V /\ B C_ C ) -> ( topGen ` B ) C_ ( topGen ` C ) ) $=
      ( vx wcel wss wa ctg cfv cv cpw cin cuni wi ssrin uniss syl cvv wb eltg
      sstr2 syl5com adantl ssexg ancoms adantr 3imtr4d ssrdv ) BCEZABFZGZDAHIZB
      HIZUKDJZAUNKZLZMZFZUNBUOLZMZFZUNULEZUNUMEZUJURVANUIUJUQUTFZURVAUJUPUSFVDA
      BUOOUPUSPQUNUQUTUAUBUCUKAREZVBURSUJUIVEABCUDUEUNARTQUIVCVASUJUNBCTUFUGUH
      $.
      $( [7-May-2007] $)

    $( A criterion for determining whether one topology is finer than another,
       based on a comparison of their bases.  Lemma 2.2 of [Munkres] p. 80. $)
    tgss2 $p |- ( ( B e. TopBases /\ C e. TopBases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) C_ ( topGen ` C ) <->
     A. x e. U. B A. y e. B ( x e. y -> E. z e. C ( x e. z /\ z C_ y ) ) ) ) $=
      ( vu vw vv ctb wcel cuni wceq ctg cfv wss cv wa wrex wi wral bastg sselda
      w3a sylan adantllr adantrl adantrr simprr syl2anc exp43 ralrimdvv 3adant3
      sstr elssuni elequ1 anbi1d rexbidv imbi12d elequ2 sseq2 anbi2d rcla42v ex
      tg2 com4l imp32 adantrrr an12s sstr2 com12 anim2d reximdv adantl ad2antll
      mpd exp32 rexlimdv syl5 impl adantll ralrimiva eltg2b syl5ibr exp4c 3comr
      3imp expdimp ssrdv impbid ) DIJZEIJZDKZEKLZUCZDMNZEMNZOZAPZBPZJZWRCPZJZXA
      WSOZQZCERZSZBDTAWLTZWJWKWQXGSWMWJWKQZWQXFABWLDXHWQWRWLJZWSDJZQZWTXEXHWQQZ
      XKWTQQWSWPJZWTXEXLXKXMWTXLXJXMXIWJWQXJXMWKWJWQQDWPWSWJDWOOWQDWPODIUADWOWP
      UMUDUBUEUFUGXLXKWTUHCWSEWRVDUIUJUKULWNXGWQWNXGQFWOWPWNXGFPZWOJZXNWPJZWKWM
      WJXGXOQZXPSZWKWMWJXRWKWMWJXQXPWMWJQXQQXPWKGPZXAJZXAXNOZQZCERZGXNTZWJXQYDW
      MWJXQQYCGXNXQXSXNJZYCWJXGXOYEYCXOYEQXSHPZJZYFXNOZQZHDRXGYCHXNDXSVDXGYIYCH
      DXGYFDJZYIYCXGYJYIQQXTXAYFOZQZCERZYCYJXGYIYMYJXGYIQQXSWLJZYMYJYIYNXGYJYGY
      NYHYJYFWLXSYFDUNUBUGUFYJXGYGYNYMSZYHYJXGYGYOYNYJXGYGYMYNYJXGYGYMSZSXFYPXS
      WSJZXTXCQZCERZSABXSYFWLDWRXSLZWTYQXEYSAGBUOYTXDYRCEYTXBXTXCAGCUOUPUQURWSY
      FLZYQYGYSYMBHGUSUUAYRYLCEUUAXCYKXTWSYFXAUTVAUQURVBVCVEVFVGVOVHYIYMYCSZXGY
      JYHUUBYGYHYLYBCEYHYKYAXTYKYHYAXAYFXNVIVJVKVLVMVNVOVPVQVRVSVTWAVTGCXNEIWBW
      CWDWFWEWGWHVCWI $.
      $( [20-Jul-2006] $)

    $( A criterion for determining whether one topology is finer than another.
       Lemma 2.2 of [Munkres] p. 80 using abbreviations. $)
    tgss3 $p |- ( ( B e. TopBases /\ C e. TopBases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) C_ ( topGen ` C ) <->
                  A. x e. B ( U. B i^i x ) C_ U. ( C i^i ~P x ) ) ) $=
      ( vy vz ctb wcel cuni ctg cfv wss cv wa wi wral cin wal elin wex bitri
      wceq w3a wrex tgss2 ralcom anass anbi1i ancom bitr3i elpw anbi2i 3bitr3ri
      cpw exbii df-rex eluni 3bitr4ri imbi12i impexp albii df-ral ralbii syl6bb
      vex dfss2 ) BFGCFGBHZCHUAUBBIJCIJKDLZALZGZVGELZGZVJVHKZMZECUCZNZABODVFOZV
      FVHPZCVHUMZPZHZKZABOZDAEBCUDVPVODVFOZABOWBVODAVFBUEWCWAABVGVQGZVGVTGZNZDQ
      VGVFGZVONZDQWAWCWFWHDWFWGVIMZVNNWHWDWIWEVNVGVFVHRVJCGZVMMZESVKVJVSGZMZESV
      NWEWKWMEWJVJVRGZMZVKMZWJWNVKMZMWMWKWJWNVKUFWPWLVKMWMWLWOVKVJCVRRUGWLVKUHU
      IWQVMWJWQVKWNMVMWNVKUHWNVLVKVJVHEVDUJUKTUKULUNVMECUOEVGVSUPUQURWGVIVNUSTU
      TDVQVTVEVODVFVAUQVBTVC $.
      $( [3-Apr-2008] $) $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the third
       antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81. $)
    basgen2 $p |- ( ( J e. Top /\ B C_ J /\ A. x e. J A. y e. x E. z e. B
            ( y e. z /\ z C_ x ) ) -> ( B e. TopBases /\ ( topGen ` B ) = J ) )
        $=
      ( vw vv ctop wcel wss cv wa wrex wral wceq wi imp wb cuni ralimi ctb ssel
      ctg cfv anim12d inopn 3expb sylan2 anassrs sseq2 anbi2d raleqbi1dv rcla4v
      cin rexbidv syl ralrimdvva cvv ssexg ancoms isbasis2g sylibrd eltg3 simpr
      adantl sstr uniopn adantr eqeltrd exp42 com23 imp4b exlimdv sylbid syldan
      ssrdv tgtop ad2antrr topbas eqid topopn simpl reximi raleq elequ1 rcla4cv
      wex eluni2 syl6ibr syl6 syl2im adantlr uniss ad2antlr eqssd jca31 ralimi2
      ax-1 ralcom sylib tgss2 3expa biimpar syl2anc eqsstr3d jca 3impa ) EHIZDE
      JZBKZCKZIZXKAKZJZLZCDMZBXMNZAENZDUAIZDUCUDZEOZLXHXILZXRLZXSYAYBXRXSYBXRXL
      XKFKZGKZUNZJZLZCDMZBYFNZGDNFDNZXSYBXRYJFGDDYBYDDIZYEDIZLZLYFEIZXRYJPXHXIY
      NYOXIYNLXHYDEIZYEEIZLZYOXIYNYRXIYLYPYMYQDEYDUBDEYEUBUEQXHYPYQYOYDYEEUFUGU
      HUIXQYJAYFEXPYIBXMYFXMYFOZXOYHCDYSXNYGXLXMYFXKUJUKUOULUMUPUQYBDURIZXSYKRX
      IXHYTDEHUSUTFGBCDURVAUPVBQZYCXTEYBXRXSXTEJUUAYBXSLZAXTEUUBXMXTIZXJDJZXMXJ
      SZOZLZBWGZXMEIZXSUUCUUHRYBBXMDVCVEYBUUHUUIPXSYBUUGUUIBXHXIUUDUUFUUIXHUUDX
      IUUFUUIPXHUUDXIUUFUUIXHUUDXILZLZUUFLXMUUEEUUKUUFVDUUKUUEEIZUUFUUJXHXJEJUU
      LXJDEVFXJEVGUHVHVIVJVKVLVMVHVNVPVOYCEEUCUDZXTXHUUMEOXIXREVQVRYCEUAIZXSLES
      ZDSZOZLZXJXMIXPPZAENBUUONZUUMXTJZYCUUNXSUUQXHUUNXIXREVSVRUUAYCUUOUUPXHXRU
      UOUUPJXIXHXRLFUUOUUPXHXRYDUUOIZYDUUPIZPZXHUUOEIZXRXLCDMZBXMNZAENZUVDEUUOU
      UOVTWAXQUVGAEXPUVFBXMXOXLCDXLXNWBWCTTUVEUVHUVFBUUONZUVDUVGUVIAUUOEUVFBXMU
      UOWDUMUVIUVBYDXKIZCDMZUVCUVFUVKBYDUUOXJYDOXLUVJCDBFCWEUOWFCYDDWHWIWJWKQVP
      WLXIUUPUUOJXHXRDEWMWNWOWPXRUUTYBXRUUSBUUONZAENUUTXQUVLAEXPUUSBXMUUOUUSXJU
      UOIWRWQTUUSABEUUOWSWTVEUURUVAUUTUUNXSUUQUVAUUTRBACEDXAXBXCXDXEWOXFXG $.
      $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the third
       antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81 using
       abbreviations. $)
    basgen $p |- ( ( J e. Top /\ B C_ J /\ A. x e. J
          x C_ U. ( B i^i ~P x ) ) -> ( B e. TopBases /\ ( topGen ` B ) = J ) )
        $=
      ( vy vz cv cpw cin cuni wss wral ctop wcel wa ctb wex anbi2i bitri ralbii
      wrex ctg cfv wceq dfss3 elin elpw an12 exbii eluni df-rex 3bitr4i basgen2
      vex syl3an3b ) AFZBUOGZHZIZJZACKCLMBCJDFZEFZMZVAUOJZNZEBTZDUOKZACKBOMBUAU
      BCUCNUSVFACUSUTURMZDUOKVFDUOURUDVGVEDUOVBVAUQMZNZEPVABMZVDNZEPVGVEVIVKEVI
      VBVJVCNZNVKVHVLVBVHVJVAUPMZNVLVABUPUEVMVCVJVAUOEUMUFQRQVBVJVCUGRUHEUTUQUI
      VDEBUJUKSRSADEBCULUN $.
      $( [16-Apr-2008] $) $( [22-Jul-2006] $)

    $( Conditions that determine the equality of two generated topologies. $)
    2basgen $p |- ( ( ( B e. TopBases /\ C e. TopBases ) /\ ( B C_ C /\ C C_
                  ( topGen ` B ) ) ) -> ( topGen ` B ) = ( topGen ` C ) ) $=
      ( ctb wcel wa wss ctg cfv tgss ad2ant2lr ctop tgcl topbas sylan ad2ant2rl
      syl wceq tgidm ad2antrr sseqtrd eqssd ) ACDZBCDZEABFZBAGHZFZEZEZUEBGHZUCU
      DUEUIFUBUFABCIJUHUIUEGHZUEUBUFUIUJFZUCUDUBUECDZUFUKUBUEKDULALUEMPBUECINOU
      BUJUEQUCUGARSTUA $.
      $( [8-May-2007] $)

    $( If a subbase is included into a topology, so is the generated topology.
       (Contributed by FL, 20-Apr-2012.)  (The proof was shortened by Mario
       Carneiro, 10-Jan-2015.) $)
    tgfiss $p |- ( ( J e. Top /\ A C_ J ) -> ( topGen ` ( fi ` A ) ) C_ J ) $=
      ( ctop wcel wss wa cfi cfv ctg fiss wceq fitop adantr sseqtrd tgss syldan
      tgtop ) BCDZABEZFZAGHZIHZBIHZBRSUABEUBUCETUABGHZBABCJRUDBKSBLMNUABCOPRUCB
      KSBQMN $.
      $( [10-Jan-2015] $) $( [20-Apr-2012] $)
  $}

  ${
    $d u v w x y z B $.  $d v w x y z J $.
    $( A subset of a topology is a basis for the topology iff every member of
       the topology is a union of members of the basis.  We use the
       idiom " ` B e. TopBases /\ ( topGen `` B ) = J ` " to express " ` B ` is
       a basis for topology ` J ` ," since we do not have a separate notation
       for this.  Definition 15.35 of [Schechter] p. 428. $)
    bastop1 $p |- ( ( J e. Top /\ B C_ J ) -> ( ( B e. TopBases /\
      ( topGen ` B ) = J ) <-> A. x e. J E. y ( y C_ B /\ x = U. y ) ) ) $=
      ( vz vw ctop wcel wss wa ctb ctg cfv wceq cuni wral eleq2 ralrimiv wrex
      cv wex bicomd eltg3 sylan9bbr biimpd wi eluni2 elssuni biimpar sylan2 a1d
      sseq2 reximdva syl5bi sylbid ssrexv sylan9r exlimiv ralimi basgen2 3expia
      ancld syl5 impbid2 ) DGHZCDIZJZCKHZCLMZDNZJZBTZCIZATZVLOZNZJZBUAZADPZVKVR
      ADVKVNDHZVRVJVTVNVIHZVHVRVJWAVTVIDVNQUBBVNCUCUDUERVSETZFTZHZWCVNIZJZFCSZE
      VNPZADPZVGVKVRWHADVRWGEVNVQWBVNHZWGUFBVPWJWFFVLSZVMWGVPWJWBVOHZWKVNVOWBQW
      LWDFVLSVPWKFWBVLUGVPWDWFFVLVPWCVLHZJZWDWEWNWEWDWMVPWCVOIZWEWCVLUHVPWEWOVN
      VOWCULUIUJUKVBUMUNUOWFFVLCUPUQURRUSVEVFWIVKAEFCDUTVAVCVD $.
      $( [2-Feb-2008] $)

    $( A version of ~ bastop1 that doesn't have ` B C_ J ` in the
       antecedent. $)
    bastop2 $p |- ( J e. Top -> ( ( B e. TopBases /\ ( topGen ` B ) = J ) <->
           ( B C_ J /\ A. x e. J E. y ( y C_ B /\ x = U. y ) ) ) ) $=
      ( ctb wcel ctg cfv wceq wa wss ctop cv cuni wex wral bastg adantr sseqtrd
      simpr pm4.71ri bastop1 pm5.32da syl5bb ) CEFZCGHZDIZJZCDKZUHJDLFZUIBMZCKA
      MUKNIJBOADPZJUHUIUHCUFDUECUFKUGCEQRUEUGTSUAUJUIUHULABCDUBUCUD $.
      $( [3-Feb-2008] $)
  $}

