$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Compactness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Comp $.

  $( Extend class notation with the class of all compact spaces. $)
  ccmp $a class Comp $.

  ${
    $d x y z $.
    $( Definition of a compact topology.  A topology is compact iff any open
       covering of its underlying set contains a finite sub-covering
       (Heine-Borel property).  Bourbaki TG I.59 prop C''' .  Note:  Bourbaki
       uses the term quasi-compact topology but it is not the modern usage
       (which we follow).  (Contributed by FL, 22-Dec-2008.) $)
    df-cmp $a |- Comp = { x e. Top | A. y e. ~P x ( U. x = U. y
      -> E. z e. ( ~P y i^i Fin ) U. x = U. z ) } $.
  $}

  ${
    $d J x y z $.
    $( The predicate "is a compact topology".  (Contributed by FL,
       22-Dec-2008.) $)
    iscmp $p |- ( J e. Comp <-> ( J e. Top /\ A. y e. ~P J
      ( U. J = U. y -> E. z e. ( ~P y i^i Fin ) U. J = U. z ) ) ) $=
      ( vx cv cuni wceq cpw cfn cin wrex wi wral ctop ccmp unieq eqeq1d rexbidv
      pweq imbi12d raleqbidv df-cmp elrab2 ) DEZFZAEZFZGZUEBEFZGZBUFHIJZKZLZAUD
      HZMCFZUGGZUOUIGZBUKKZLZACHZMDCNOUDCGZUMUSAUNUTUDCSVAUHUPULURVAUEUOUGUDCPZ
      QVAUJUQBUKVAUEUOUIVBQRTUADABUBUC $.
      $( [1-Dec-2010] $) $( [22-Dec-2008] $)
  $}

  ${
    $d c d o r s u v x y F $.  $d c d o r s u v x y J $.
    $d c d o r s u v x y K $.  $d c d o r s u v x y X $.
    $d c d o r s u v x y Y $.
    cncmp.1 $e |- X = U. J $.
    cncmp.2 $e |- Y = U. K $.
    $( Compactness is respected by a continuous onto map.  (Contributed by Jeff
       Hankins, 12-Jul-2009.) $)
    cncmp $p |- ( ( ( J e. Comp /\ K e. Top ) /\
                    ( F : X -onto-> Y /\ F e. ( J Cn K ) ) ) -> K e. Comp ) $=
      ( vs vy vd vc wcel wa cuni cv wceq cfn wrex wi cima vu vv vr vx ccmp ctop
      vo wfo ccn co cpw cin wral simplr iscmp wss vex elpw w3a ccnv cab rexbidv
      eqeq1 elab simp3 ssel2 cnima ex 3expa imp adantlrl sylan2 anassrs 3adant3
      3adantl2 eqeltrd rexlimdv syl5bi ssrdv abrexex sylibr unieq eqeq2d ineq1d
      3exp pweq rexeqdv imbi12d rcla4v syl ciun cdm crn wfn fofn cnvimarndm a1i
      fndm simpr imaeq2d simpl 3eqtr3rd imaeq2i 3eqtr3g syl2anc ad2antrl imaeq2
      forn 3ad2ant1 3ad2ant2 imauni syl6eq cvv cnvexg imaexg ad2antll ralrimivw
      eqtrd dfiun2g elin anbi1i bitri ssel eqtr elssuni syl5ibr sseq2i foimacnv
      sseq2 eqcomd sylan2br eleq1 biimprd syl6bir com13 com4t syl6 a1d sylanbrc
      sylib com14 sylcom com12 com15 syl5 rexlimiv sylbi imp31 3adantl3 adantrr
      copab rnopab2 cdom eqid fnopab2g dffn4 fodomfi ancoms sylan domfi adantll
      mpdan syl5eqelr adantld 3imp1 foima 3ad2ant3 rcla4ev embantd com24 syl7bi
      wbr syld ralrimdv exp43 com4l imp5d exp31 com4r imp43 ) BUELZCUFLZMDEAUHZ
      ABCUIUJZLZMZMUWBCNZUAOZNZPZUWGUBOZNZPZUBUWHUKZQULZRZSZUACUKZUMZCUELUWAUWB
      UWFUNUWAUWBUWCUWEUWSUWBUWCUWEUWAUWSUWBUWCUWEUWAUWSSUWABUFLZBNZUCOZNZPZUXA
      HOZNZPZHUXBUKZQULZRZSZUCBUKZUMZMUWBUWCMUWEMUWSUCHBUOUWBUWCUWEUWTUXMUWSUWT
      UWBUWCUWEUXMUWSSZUWTUWBUWCUWEUXNUWTUWBMZUWFMZUXMUWQUAUWRUWHUWRLUWHCUPZUXP
      UXMUWQUWHCUAUQZURUXPUWJUXQUXMUWPUXPUWJUXQUXMUWPSUXPUWJUXQUSZUXMUXAUDOZAUT
      ZIOZTZPZIUWHRZUDVAZNZPZUXGHUYFUKZQULZRZSZUWPUXSUYFUXLLZUXMUYLSUXSUYFBUPUY
      MUXSUGUYFBUGOZUYFLUYNUYCPZIUWHRZUXSUYNBLZUYEUYPUDUYNUGUQZUXTUYNPUYDUYOIUW
      HUXTUYNUYCVCVBVDUXSUYOUYQIUWHUXSUYBUWHLZUYOUYQUXSUYSUYOUSUYNUYCBUXSUYSUYO
      VEUXSUYSUYCBLZUYOUXPUXQUYSUYTUWJUXPUXQUYSUYTUXQUYSMUXPUYBCLZUYTUWHCUYBVFU
      XOUWEVUAUYTUWCUXOUWEMVUAUYTUWTUWBUWEVUAUYTSUWTUWBUWEUSVUAUYTUYBABCVGVHVIV
      JVKVLVMVOVNVPWEVQVRVSUYFBIUDUWHUYCUXRVTURWAUXKUYLUCUYFUXLUXBUYFPZUXDUYHUX
      JUYKVUBUXCUYGUXAUXBUYFWBWCVUBUXGHUXIUYJVUBUXHUYIQUXBUYFWFWDWGWHWIWJUXSUYH
      UYKUWPUXSUXAIUWHUYCWKZUYGUXSUXAUYAUWITZVUCUXSUXAUYAUWGTZVUDUXPUWJUXAVUEPZ
      UXQUWCVUFUXOUWEUWCAWLZDPZAWMZEPZVUFUWCADWNVUHDEAWODAWRWJDEAXHVUHVUJMZDUYA
      ETZUXAVUEVUKUYAVUITZVUGVULDVUMVUGPVUKAWPWQVUKVUIEUYAVUHVUJWSWTVUHVUJXAXBF
      EUWGUYAGXCXDXEXFXIUWJUXPVUEVUDPUXQUWGUWIUYAXGXJXRIUYAUWHXKXLUXSUYCXMLZIUW
      HUMVUCUYGPUXSVUNIUWHUXPUWJVUNUXQUWEVUNUXOUWCUWEUYAXMLVUNAUWDXNUYAUYBXMXOW
      JXPXIXQIUDUWHUYCXMXSWJXRUXSUXGUWPHUYJUXEUYJLZUXEUYFUPZUXEQLZMZUXSUXGUWPSV
      UOUXEUYILZVUQMVURUXEUYIQXTVUSVUPVUQUXEUYFHUQZURYAYBUXSVURUXGUWPUXSVURUXGU
      SZJOZAKOZTZPZKUXERZJVAZUWOLZUWGVVGNZPZUWPVVAVVGUWHUPZVVGQLZVVHVVAUGVVGUWH
      UYNVVGLUYNVVDPZKUXERZVVAUYNUWHLZVVFVVNJUYNUYRVVBUYNPVVEVVMKUXEVVBUYNVVDVC
      VBVDVVAVVMVVOKUXEUXSVURVVCUXELZVVMVVOSZSZUXGUXSVUPVVRVUQUXPUWJVUPVVRUXQUX
      PUWJVUPVVRVUPVVPUXPUWJVVQVUPVVPVVCUYFLZUXPUWJVVQSSZUXEUYFVVCYCVVSVVCUYCPZ
      IUWHRZVVTUYEVWBUDVVCKUQUXTVVCPUYDVWAIUWHUXTVVCUYCVCVBVDVWAVVTIUWHVWAVVDAU
      YCTZPZUYSVVTVVCUYCAXGVVMVWDUXPUWJUYSVVOVVMVWDUYNVWCPZUXPUWJUYSVVOSZSZSVVM
      VWDVWEUYNVVDVWCYDVHUXPVWEVWGUWCVWEVWGSUXOUWEUWJUYSUWCVWEVVOUWJUYSUYBUWGUP
      ZUWCVWEVVOSSUYSVWHUWJUYBUWIUPUYBUWHYEUWGUWIUYBYIYFVWEVWHUWCUYSVVOUWCVWHVW
      EVWFUWCVWHVWEVWFSUWCVWHMZVWEUYNUYBPZVWFVWIUYBVWCUYNVWHUWCUYBEUPZUYBVWCPEU
      WGUYBGYGUWCVWKMVWCUYBDEUYBAYHYJYKWCVWJVVOUYSUYNUYBUWHYLYMYNVHYOUUAUUBYPXF
      UUCYQUUDUUEUUFUUGYQYPUUHUUIUUJVNVQVRVSUXSVURVVLUXGUXPUWJUXQVURVVLUXPUXQVU
      RVVLSZSUWJUXPVWLUXQUWEVWLUXOUWCUWEVUQVVLVUPUWEVUQVVLUWEVUQMZVVGVVPVVEMKJU
      UKZWMZQKJUXEVVDUULVWMVWOUXEUUMUVLZVWOQLZUWEUXEVWOVWNUHZVUQVWPUWEVWNUXEWNZ
      VWRUWEVVDXMLZKUXEUMZVWSUWEVWTKUXEAVVCUWDXOZXQKJUXEVVDVWNVWNUUNUUOYTUXEVWN
      UUPYTVUQVWRVWPUXEVWOVWNUUQUURUUSVUQVWPVWQUWEUXEVWOUUTUVAUVBUVCVHUVDXPYRYR
      UVEVNVVHVVGUWNLZVVLMVVKVVLMVVGUWNQXTVXCVVKVVLVVGUWHKJUXEVVDVUTVTURYAYBYSV
      VAUWGKUXEVVDWKZVVIVVAUWGAUXFTZVXDVVAUWGAUXATZVXEUXSVURUWGVXFPZUXGUXPUWJVX
      GUXQUWCVXGUXOUWEUWCADTZEVXFUWGDEAUVFVXHVXFPUWCDUXAAFXCWQEUWGPUWCGWQXBXFXI
      XIUXGUXSVXFVXEPVURUXAUXFAXGUVGXRKAUXEXKXLVVAVXAVXDVVIPVVAVWTKUXEUXSVURVWT
      UXGUXPUWJVWTUXQUWEVWTUXOUWCVXBXPXIXIXQKJUXEVVDXMXSWJXRUWMVVJUBVVGUWOUWKVV
      GPUWLVVIUWGUWKVVGWBWCUVHXEWEVRVQUVIUVMWEUVJUVKUVNUVOUVPUVQVRUVRUVSUVTUAUB
      CUOYS $.
      $( [1-Dec-2010] $) $( [12-Jul-2009] $)
  $}

  ${
    $d J y z $.
    $( A finite topology is compact.  (Contributed by FL, 22-Dec-2008.) $)
    fincmp $p |- ( J e. ( Top i^i Fin ) -> J e. Comp ) $=
      ( vy vz ctop cfn cin wcel cuni cv wceq cpw wrex wi wral inss1 sseli inss2
      ccmp wa vex pwid wss elpw ssfi sylan2b elin eqeq2d rcla4ev sylbir sylancr
      unieq ex ralrimiva syl iscmp sylanbrc ) ADEFZGZADGAHZBIZHZJZUSCIZHZJZCUTK
      ZEFZLZMZBAKZNZARGUQDADEOPURAEGZVKUQEADEQPVLVIBVJVLUTVJGZSUTVFGZUTEGZVIUTB
      TZUAVMVLUTAUBVOUTAVPUCAUTUDUEVNVOSUTVGGZVIUTVFEUFVQVBVHVEVBCUTVGVCUTJVDVA
      USVCUTUKUGUHULUIUJUMUNBCAUOUP $.
      $( [13-Jun-2011] $)
  $}

  $( The singleton of the empty set is compact.  (Contributed by FL,
     2-Aug-2009.) $)
  0cmp $p |- { (/) } e. Comp $=
    ( c0 csn ctop cfn cin wcel ccmp elin sn0top snfi mpbir2an fincmp ax-mp ) AB
    ZCDEFZNGFONCFNDFNCDHIAJKNLM $.
    $( [2-Aug-2009] $)

  ${
    $d r s J $.
    $( A compact topology is a topology.  (Contributed by Jeff Hankins,
       29-Jun-2009.) $)
    cmptop $p |- ( J e. Comp -> J e. Top ) $=
      ( vr vs ccmp wcel ctop cuni cv wceq cpw cfn cin wrex wral iscmp simplbi
      wi ) ADEAFEAGZBHZGIRCHGICSJKLMQBAJNBCAOP $.
      $( [29-Jun-2009] $)
  $}

  ${
    $d f s u x y z A $.  $d r s u x y J $.  $d f u x ph $.  $d s u z ps $.
    $d r s S $.  $d u x X $.
    cmpcov.1 $e |- X = U. J $.
    $( An open cover of a compact topology has a finite subcover.  (Contributed
       by Jeff Hankins, 29-Jun-2009.) $)
    cmpcov $p |- ( ( J e. Comp /\ S C_ J /\ X = U. S ) ->
                    E. s e. ( ~P S i^i Fin ) X = U. s ) $=
      ( vr ccmp wcel wss cuni wceq cv cpw cfn cin wrex wa wi cvv eqeq1i wral wb
      simpr ssexg ancoms elpwg syl mpbird ctop iscmp simprbi adantr eqeq2d pweq
      unieq ineq1d rexeqdv imbi12d rcla4v sylc rexbii 3imtr4g 3impia ) BGHZABIZ
      CAJZKZCDLJZKZDAMZNOZPZVDVEQZBJZVFKZVNVHKZDVKPZVGVLVMABMZHZVNFLZJZKZVPDVTM
      ZNOZPZRZFVRUAZVOVQRZVMVSVEVDVEUCVMASHZVSVEUBVEVDWIABGUDUEABSUFUGUHVDWGVEV
      DBUIHWGFDBUJUKULWFWHFAVRVTAKZWBVOWEVQWJWAVFVNVTAUOUMWJVPDWDVKWJWCVJNVTAUN
      UPUQURUSUTCVNVFETVIVPDVKCVNVHETVAVBVC $.
      $( [29-Jun-2009] $)

    $d s X $.
    cmpcovf.2 $e |- ( z = ( f ` y ) -> ( ph <-> ps ) ) $.
    $( Combine ~ cmpcov with ~ ac6sfi to show the existence of a function that
       indexes the elements that are generating the open cover. $)
    cmpcovf $p |- ( ( J e. Comp /\
      A. x e. X E. y e. J ( x e. y /\ E. z e. A ph ) ) ->
      E. s e. ( ~P J i^i Fin ) ( X = U. s /\
        E. f ( f : s --> A /\ A. y e. s ps ) ) ) $=
      ( vu wcel cv wrex wa wral cfn wss ccmp cuni wceq crab cpw wf simpl ssrab2
      cin wex a1i simpr dfss3 elunirab ralbii bitri sylibr syl syl6sseqr cmpcov
      uniss eqssd syl3anc wi elin vex elpw ssrab anbi1i simp2ll simp2r sylanbrc
      w3a simp3 simp2lr ac6sfi syl2anc unieq eqeq2d feq2 anbi12d exbidv rcla4ev
      raleq syl12anc 3expia sylan2b rexlimdva sylc ) HUANZCOZDONAEFPZQDHPZCIRZQ
      ZWJIMOZUBZUCZMWLDHUDZUEZSUIZPZIJOZUBZUCZXCFGOZUFZBDXCRZQZGUJZQZJHUEZSUIZP
      ZWJWNUGZWOWJWSHTZIWSUBZUCXBXOXPWOWLDHUHUKZWOIXQWOWNIXQTZWJWNULXSWKXQNZCIR
      WNCIXQUMXTWMCIWLDWKHUNUOUPUQWOXQHUBZIWOXPXQYATXRWSHVAURKUSVBWSHIMKUTVCWJW
      RXNMXAWPXANZWJWPHTZWLDWPRZQZWPSNZQZWRXNVDYBWPWTNZYFQYGWPWTSVEYHYEYFYHWPWS
      TYEWPWSMVFZVGWLDHWPVHUPVIUPWJYGWRXNWJYGWRVMZWPXMNZWRWPFXFUFZBDWPRZQZGUJZX
      NYJWPXLNZYFYKYJYCYPYCYDYFWJWRVJWPHYIVGUQWJYEYFWRVKZWPXLSVEVLWJYGWRVNYJYFY
      DYOYQYCYDYFWJWRVOABDEWPFGLVPVQXKWRYOQJWPXMXCWPUCZXEWRXJYOYRXDWQIXCWPVRVSY
      RXIYNGYRXGYLXHYMXCWPFXFVTBDXCWPWDWAWBWAWCWEWFWGWHWI $.
      $( [14-Sep-2014] $)
  $}

  ${
    $d c d f s t u v w x y z J $.  $d c d f s t u v w x y z S $.
    $d c d f s t u v w x y z X $.
    cmpsub.1 $e |- X = U. J $.
    $( Lemma for ~ cmpsub . $)
    cmpsublem $p |- ( ( J e. Top /\ S C_ X ) ->
    ( A. c e. ~P J ( S C_ U. c -> E. d e. ( ~P c i^i Fin ) S C_ U. d ) ->
      A. s e. ~P ( subSp ` <. S , J >. ) ( U. ( subSp ` <. S , J >. ) = U. s ->
      E. t e. ( ~P s i^i Fin ) U. ( subSp ` <. S , J >. ) = U. t ) ) ) $=
      ( vy vx vz wcel wss wa cv cuni cfn cin wi wceq vv vu vw ctop cpw wrex cop
      wral csubsp cfv crab cvv rabexg ad2antrr ssrab2 elpwg mpbiri unieq sseq2d
      pweq ineq1d rexeqdv imbi12d rcla4va sylan ex sseq2i stoig2 sylan2b adantr
      syl eqeq1d wex elpw wb eleq2 eluni syl6bb adantl ssel uniexg ssexg ancoms
      vex elsubsp syldan w3a inss1 sseq1 sselda 3ad2antl3 3adant2 simp12 biimpa
      eleq1 3adant3 ineq1 eleq1d elrab sylanbrc anbi12d cla4ev syl2anc rexlimdv
      sylbid com23 com4l sylcom com24 impcom imp3a exlimdv syl6ibr ssrdv pm2.27
      3exp imp elin cab eqeq1 rexbidv elab eleq1a sylbi syl6 sylanb 3imp syl5bi
      a1d abrexex sylibr abrexfi ad2antlr ciun dfss biimpi uniiun ineq2i iunin2
      a1i incom iuneq2i 3eqtr2i syl6eq 3ad2ant2 ad2antrl 3adant1 dfiun2 3eqtr3d
      inex1 eqeq2d rcla4ev rexlimiv com3r mpd sylbird syld ralrimdva ) CUDLZBDM
      ZNZBFOZPZMZBGOZPZMZGUVBUEZQRZUFZSZFCUEZUHZBCUGUIUJZPZEOZPZTZUVOAOZPZTZAUV
      PUEZQRZUFZSZEUVNUEZUVAUVPUWFLZNZUVMBIOZBRZUVPLZICUKZPZMZUVGGUWLUEZQRZUFZS
      ZUWEUWHUVMUWRUWHUWLUVLLZUVMUWRUWHUWLULLZUWSUUSUWTUUTUWGUWKICUDUMUNUWTUWSU
      WLCMUWKICUOUWLCULUPUQVKUVKUWRFUWLUVLUVBUWLTZUVDUWNUVJUWQUXAUVCUWMBUVBUWLU
      RUSUXAUVGGUVIUWPUXAUVHUWOQUVBUWLUTVAVBVCVDVEVFUWHUVRUWRUWDUWHUVRBUVQTZUWR
      UWDSZUWHBUVOUVQUVABUVOTZUWGUUTUUSBCPZMZUXDDUXEBHVGZBCVHVIVJZVLUWHUXBUXCUW
      HUXBNZUWNUXCUXIABUWMUXIUVSBLZUVSUAOZLZUXKUWLLZNZUAVMZUVSUWMLUWHUXBUXJUXOS
      ZUWGUVAUVPUVNMZUXBUXPSUVPUVNEWDVNUVAUXQNZUXBUXPUXRUXBNUXJUVSUBOZLZUXSUVPL
      ZNZUBVMZUXOUXBUXJUYCVOUXRUXBUXJUVSUVQLUYCBUVQUVSVPUBUVSUVPVQVRVSUXRUYCUXO
      SUXBUXRUYBUXOUBUXRUXTUYAUXOUXQUVAUXTUYAUXOSSUXQUYAUXTUVAUXOUXQUYAUXSUVNLZ
      UXTUVAUXOSSUVPUVNUXSVTUVAUYAUYDUXTUXOUVAUYDUYAUXTUXOSZUVAUYDUXSUCOZBRZTZU
      CCUFZUYAUYESZUUSUUTBULLZUYDUYIVOUUTUUSUXFUYKUXGUUSUXEULLZUXFUYKCUDWAUXFUY
      LUYKBUXEULWBWCVEVIUCUXSBCULWEWFUVAUYHUYJUCCUVAUYFCLZUYHUYJUVAUYMUYHWGZUYA
      UXTUXOUYNUYAUXTWGZUVSUYFLZUYFUWLLZUXOUYNUXTUYPUYAUYHUVAUXTUYPUYMUYHUXSUYF
      UVSUYHUXSUYFMUYGUYFMUYFBWHUXSUYGUYFWIUQWJWKWLUYOUYMUYGUVPLZUYQUVAUYMUYHUY
      AUXTWMUYNUYAUYRUXTUYHUVAUYAUYRUYMUYHUYAUYRUXSUYGUVPWOWNWKWPUWKUYRIUYFCUWI
      UYFTUWJUYGUVPUWIUYFBWQWRWSWTUXNUYPUYQNUAUYFUCWDUXKUYFTUXLUYPUXMUYQUXKUYFU
      VSVPUXKUYFUWLWOXAXBXCXPXPXDXEXFXGXHXIXJXKXLVJXEVFVIXQUAUVSUWLVQXMXNUWNUWR
      UXIUWDUWNUWRUWQUXIUWDSZUWNUWQXOUVGUYSGUWPUVEUWPLUVEUWOLZUVEQLZNZUVGUYSSUV
      EUWOQXRVUBUVGUXIUWDVUBUVGUXIWGZJOZKOZBRZTZKUVEUFZJXSZUWCLZUVOVUIPZTZUWDVU
      CVUIUWBLZVUIQLZVUJVUCVUIUVPMVUMVUCAVUIUVPUVSVUILUVSVUFTZKUVEUFZVUCUVSUVPL
      ZVUHVUPJUVSAWDVUDUVSTVUGVUOKUVEVUDUVSVUFXTYAYBVUCVUOVUQKUVEVUBUVGUXIVUEUV
      ELZVUOVUQSZSZUYTUVEUWLMZVUAUVGUXIVUTSZSZUVEUWLGWDZVNVVAVVCVUAVVAVVBUVGVVA
      VUTUXIVVAVURVUEUWLLZVUSUVEUWLVUEVTVVEVUECLZVUFUVPLZNVUSUWKVVGIVUECUWIVUET
      UWJVUFUVPUWIVUEBWQWRWSVVGVUSVVFVUFUVPUVSYCVSYDYEYIYIVJYFYGXDYHXNVUIUVPKJU
      VEVUFVVDYJVNYKVUBUVGVUNUXIVUAVUNUYTUVGKJUVEVUFYLYMWPVUIUWBQXRWTVUCBKUVEVU
      FYNZUVOVUKUVGVUBBVVHTUXIUVGBBUVFRZVVHUVGBVVITBUVFYOYPVVIBKUVEVUEYNZRKUVEB
      VUERZYNVVHUVFVVJBKUVEYQYRKUVEBVUEYSKUVEVVKVUFVVKVUFTVURBVUEUUAYTUUBUUCUUD
      UUEUVGUXIUXDVUBUWHUXDUVGUXBUXHUUFUUGVVHVUKTVUCKJUVEVUFVUEBKWDUUJUUHYTUUIU
      WAVULAVUIUWCUVSVUITUVTVUKUVOUVSVUIURUUKUULXCXPYDUUMYEUUNUUOVFUUPXFUUQUUR
      $.
      $( [28-Jun-2009] $)

    $( Two equivalent ways of describing a compact subset of a topological
       space.  Inspired by Sue E. Goodman's _Beginning Topology_.  (Contributed
       by Jeff Hankins, 22-Jun-2009.) $)
    cmpsub $p |- ( ( J e. Top /\ S C_ X ) ->
                   ( ( subSp ` <. S , J >. ) e. Comp <-> A. c e. ~P J
                 ( S C_ U. c -> E. d e. ( ~P c i^i Fin ) S C_ U. d ) ) ) $=
      ( vs vt vy vf wcel wss wa cuni cv wceq cfn cin wrex wi vx vw vu vv csubsp
      ctop cop cfv ccmp cpw wral iscmp wb id topopn ssexg syl2anr stoig3 syldan
      cvv ibar bicomd syl syl5bb cab eqeq1 rexbidv elab elpw ssel2 ineq1 eqeq2d
      vex rcla4ev ex adantl rexlimdv simpll sseq2i uniexg sylan2 ancoms sylan2b
      sylbi adantr elsubsp syl2anc sylibrd syl5bi abrexex sylibr ineq1d rexeqdv
      ssrdv unieq pweq imbi12d rcla4va sylan stoig2 ad2antrr ciun dfiun2 eqcomi
      inex1 a1i incom iunin2 uniiun ineq2d sseqin2 biimpi syl5eq 3eqtrd 3eqtrrd
      iuneq2dv eqeq12d eqeq1d eqid a1bi dfss3 ralbii 3bitri anbi1i bitri wf wex
      elin ac6sfi crn frn ad2antrl rnex cdom wbr adantll sseq1 syld eluni com23
      simprr wfo wfn dffn4 sylib fodomfi ad2ant2r domfi sylanbrc rcla4cv pm2.27
      fveq2 inss1 mpbiri ssel a1dd 3imp fnfvelrn expcom 3ad2ant1 syl5 jcad 3exp
      ffn w3a com3r imp com3l impcom eleq2 eleq1 anbi12d cla4ev exlimdv 3imtr4g
      fvex syl6 ad2antlr mpbird jca eximdv sseq2d exlimiv mpd rexlimdva syl5bir
      syl8 sylbird ralrimdva cmpsublem impbid bitrd ) BUFKZACLZMZABUGUEUHZUIKZU
      WPNZGOZNZPZUWRHOZNZPZHUWSUJZQRZSZTZGUWPUJZUKZADOZNZLZAEOZNZLZEUXKUJZQRZSZ
      TZDBUJZUKZUWQUWPUFKZUXJMZUWOUXJGHUWPULUWOUYCUYDUXJUMUWMUWNAUTKZUYCUWNUWNC
      BKUYEUWMUWNUNBCFUOACBUPUQABUTURUSUYCUXJUYDUYCUXJVAVBVCVDUWOUXJUYBUWOUXJUX
      TDUYAUWOUXKUYAKZMZUXJUWRUAOZIOZARZPZIUXKSZUAVEZNZPZUXDHUYMUJZQRZSZTZUXTUY
      GUXJUYSUYGUYMUXIKZUXJUYSUYGUYMUWPLUYTUYGHUYMUWPUXBUYMKUXBUYJPZIUXKSZUYGUX
      BUWPKZUYLVUBUAUXBHVMZUYHUXBPUYKVUAIUXKUYHUXBUYJVFVGVHUYGVUBUXBUXNARZPZEBS
      ZVUCUYGVUAVUGIUXKUYFUYIUXKKZVUAVUGTZTZUWOUYFUXKBLZVUJUXKBDVMZVIVUKVUHVUIV
      UKVUHMUYIBKZVUIUXKBUYIVJVUMVUAVUGVUFVUAEUYIBUXNUYIPVUEUYJUXBUXNUYIAVKVLVN
      VOVCVOWDVPVQUYGUWMUYEVUCVUGUMUWMUWNUYFVRUWOUYEUYFUWNUWMABNZLZUYECVUNAFVSZ
      VUOUWMUYEUWMVUOVUNUTKUYEBUFVTAVUNUTUPWAWBWCWEEUXBABUTWFWGWHWIWNUYMUWPIUAU
      XKUYJVULWJVIWKUXHUYSGUYMUXIUWSUYMPZUXAUYOUXGUYRVUQUWTUYNUWRUWSUYMWOVLVUQU
      XDHUXFUYQVUQUXEUYPQUWSUYMWPWLWMWQWRWSVOUYGUXMUYSUXSUYGUXMUYSUXSTUYGUXMMZU
      YSAAPZAUXCPZHUYQSZTZUXSVURVUSUYOVVAUYRVURAUWRAUYNUWOAUWRPZUYFUXMUWNUWMVUO
      VVCVUPABWTWCXAZVURUYNIUXKUYJXBZIUXKAUYIRZXBZAUYNVVEPVURVVEUYNIUAUXKUYJUYI
      AIVMXEXCXDXFVURIUXKUYJVVFUYJVVFPVURVUHMUYIAXGXFXPVURVVGAIUXKUYIXBZRZAUXLR
      ZAVVGVVIPVURIUXKAUYIXHXFVURVVHUXLAVVHUXLPVURUXLVVHIUXKXIXDXFXJUXMVVJAPUYG
      UXMVVJUXLARZAAUXLXGUXMVVKAPAUXLXKXLXMVPXNXOXQVURVUTUXDHUYQVURAUWRUXCVVDXR
      VGWQVVBVVAVURUXSVUSVVAAXSXTVURVUTUXSHUYQUXBUYQKZVURUWSUYJPZIUXKSZGUXBUKZU
      XBQKZMZVUTUXSTZVVLUXBUYPKZVVPMVVQUXBUYPQYHVVSVVOVVPVVSUXBUYMLUWSUYMKZGUXB
      UKVVOUXBUYMVUDVIGUXBUYMYAVVTVVNGUXBUYLVVNUAUWSGVMUYHUWSPUYKVVMIUXKUYHUWSU
      YJVFVGVHYBYCYDYEVURVVQMZUXBUXKJOZYFZUWSUWSVWBUHZARZPZGUXBUKZMZJYGZVVRVVQV
      WIVURVVPVVOVWIVVMVWFGIUXBUXKJUYIVWDPUYJVWEUWSUYIVWDAVKVLYIWBVPVWAVWIVUTVW
      BYJZUXRKZAVWJNZLZMZJYGZUXSVWAVUTVWIVWOVWAVUTVWIVWOTVWAVUTMZVWHVWNJVWPVWHV
      WNVWPVWHMZVWKVWMVWQVWJUXQKZVWJQKZVWKVWQVWJUXKLZVWRVWCVWTVWPVWGUXBUXKVWBYK
      YLVWJUXKVWBJVMYMVIWKVWQVVPVWJUXBYNYOZVWSVWAVVPVUTVWHVURVVOVVPUUAXAVWAVWCV
      XAVUTVWGVVQVWCVXAVURVVPVWCVXAVVOVWCVVPUXBVWJVWBUUBZVXAVWCVWBUXBUUCZVXBUXB
      UXKVWBUVDZUXBVWBUUDUUEUXBVWJVWBUUFWAYPYPUUGUXBVWJUUHWGVWJUXQQYHUUIVWQVWMU
      XCVWLLZVWHVXEVWPVWHUBUXCVWLVWHUBOZUCOZKZVXGUXBKZMZUCYGVXFUDOZKZVXKVWJKZMZ
      UDYGZVXFUXCKVXFVWLKVWHVXJVXOUCVWHVXJVXFVXGVWBUHZKZVXPVWJKZMZVXOVWGVWCVXIV
      XGVXPARZPZTZVXJVXSTZVWFVYAGVXGUXBUWSVXGPZUWSVXGVWEVXTVYDUNVYDVWDVXPAUWSVX
      GVWBUULWLXQUUJVYBVWCVYCVXJVYBVWCVXSVXHVXIVYBVWCVXSTZTVXIVYBVXHVYEVXIVYBVY
      AVXHVYETVXIVYAUUKVXIVYAVXHVYEVXIVYAVXHUVEZVWCVXQVXRVXIVYAVXHVWCVXQTZVYAVX
      HVYGTZTVXIVYAVXGVXPLZVYHVYAVYIVXTVXPLVXPAUUMVXGVXTVXPYQUUNVYIVXHVXQVWCVXG
      VXPVXFUUOUUPVCXFUUQVWCVXCVYFVXRVXDVXIVYAVXCVXRTVXHVXCVXIVXRUXBVXGVWBUURUU
      SUUTUVAUVBUVCYRUVFUVGUVHUVIWAVXNVXSUDVXPVXGVWBUVPVXKVXPPVXLVXQVXMVXRVXKVX
      PVXFUVJVXKVXPVWJUVKUVLUVMUVQUVNUCVXFUXBYSUDVXFVWJYSUVOWNVPVUTVWMVXEUMVWAV
      WHAUXCVWLYQUVRUVSUVTVOUWAVOYTVWNUXSJUXPVWMEVWJUXRUXNVWJPUXOVWLAUXNVWJWOUW
      BVNUWCUWGUWDWCUWEUWFUWHVOYTYRUWIHABCGDEFUWJUWKUWL $.
      $( [15-Dec-2013] $) $( [22-Jun-2009] $)
  $}

  ${
    $d s t u v w J $.  $d s t u v w S $.
    $( A closed subset of a compact space is compact.  (Contributed by Jeff
       Hankins, 29-Jun-2009.) $)
    cmpcld $p |- ( ( J e. Comp /\ S e. ( Clsd ` J ) ) ->
                   ( subSp ` <. S , J >. ) e. Comp ) $=
      ( vs vt vu vw ccmp wcel wa cv cuni wss cpw cfn wi elpw wceq cun sylib cvv
      vv ccld cfv cop csubsp cin wrex wral vex w3a cdif csn simp1l simp2 cmptop
      ctop eqid cldopn sylan 3ad2ant1 snssd unssd simp3 uniss sstrd undif unss1
      3ad2ant2 3ad2ant3 eqsstr3d difss jctir unss eqssd uniexg ad2antrr 3adant3
      difexg unisng 3syl uneq2d eqtr4d uniun syl6eqr cmpcov syl3anc elin anbi1i
      bitri simp2l uncom syl6sseq ssundif diffi ad2antll ssexi sylanbrc sseqtrd
      wex sselda eluni simpl a1i wn simpr elndif ad2antlr eleq2 biimpd imp mtod
      com23 ex adantrd elsn notbii syl6ibr jcad eldif eximdv ssrdv unieq sseq2d
      mpd rcla4ev syl2anc syl3an2b rexlimdv syl5bi ralrimiv cldss cmpsub syldan
      3exp wb mpbird ) BGHZABUBUCHZIZABUDUEUCGHZACJZKZLZADJZKZLZDUUAMZNUFZUGZOZ
      CBMZUHZYSUUJCUUKUUAUUKHUUABLZYSUUJUUABCUIPYSUUMUUCUUIYSUUMUUCUJZBKZEJZKZQ
      ZEUUAUUOAUKZULZRZMZNUFZUGZUUIUUNYQUVABLUUOUVAKZQUVDYQYRUUMUUCUMUUNUUAUUTB
      YSUUMUUCUNUUNUUSBYSUUMUUSBHZUUCYQBUPHZYRUVFBUOZABUUOUUOUQZURUSUTVAVBUUNUU
      OUUBUUTKZRZUVEUUNUUOUUBUUSRZUVKUUNUUOUVLUUNUUOAUUSRZUVLUUNAUUOLZUVMUUOQUU
      NAUUBUUOYSUUMUUCVCZUUMYSUUBUUOLZUUCUUABVDVHZVEAUUOVFSUUCYSUVMUVLLUUMAUUBU
      USVGVIVJUUNUVPUUSUUOLZIUVLUUOLUUNUVPUVRUVQUUOAVKVLUUBUUSUUOVMSVNUUNUVJUUS
      UUBUUNUUOTHZUUSTHUVJUUSQYSUUMUVSUUCYQUVSYRUUMBGVOVPVQUUOATVRUUSTVSVTWAWBU
      UAUUTWCWDUVABUUOEUVIWEWFUUNUURUUIEUVCUUNUUPUVCHZUURUUIUVTUUNUUPUVALZUUPNH
      ZIZUURUUIUVTUUPUVBHZUWBIUWCUUPUVBNWGUWDUWAUWBUUPUVAEUIZPWHWIUUNUWCUURUJZU
      UPUUTUKZUUHHZAUWGKZLZUUIUWFUWGUUALZUWGNHZUWHUWFUUPUUTUUARZLUWKUWFUUPUVAUW
      MUUNUWAUWBUURWJUUAUUTWKWLUUPUUTUUAWMSUUNUWCUWLUURUWBUWLUUNUWAUUPUUTWNWOVQ
      UWHUWGUUGHZUWLIUWKUWLIUWGUUGNWGUWNUWKUWLUWGUUAUWGUUPUWEUUPUUTVKWPPWHWIWQU
      WFUAAUWIUWFUAJZAHZUWOFJZHZUWQUWGHZIZFWSZUWOUWIHUWFUWPUXAUWFUWPIZUWRUWQUUP
      HZIZFWSZUXAUXBUWOUUQHUXEUWFAUUQUWOUWFAUUBUUQUUNUWCUUCUURUVOUTUWFUUBUUOUUQ
      UUNUWCUVPUURUVQUTUUNUWCUURVCWRVEWTFUWOUUPXASUXBUXDUWTFUXBUXDUWRUWSUXDUWRO
      UXBUWRUXCXBXCUXBUXDUXCUWQUUTHZXDZIUWSUXBUXDUXCUXGUXDUXCOUXBUWRUXCXEXCUXBU
      XDUWQUUSQZXDZUXGUXBUWRUXIUXCUXBUWRUXIUXBUWRIUXHUWOUUSHZUWPUXJXDUWFUWRUWOA
      UUOXFXGUXBUWRUXHUXJOUXBUXHUWRUXJUXHUWRUXJOOUXBUXHUWRUXJUWQUUSUWOXHXIXCXLX
      JXKXMXNUXFUXHFUUSXOXPXQXRUWQUUPUUTXSXQXRXTYDXMFUWOUWGXAXQYAUUFUWJDUWGUUHU
      UDUWGQUUEUWIAUUDUWGYBYCYEYFYGYNYHYDYNYIYJYQUVGYRYTUULYOZUVHUVGYRUVNUXKABU
      UOUVIYKABUUOCDUVIYLYMUSYP $.
      $( [29-Jun-2009] $)
  $}

  ${
    $d c d m n r s J $.  $d c d m n r s S $.  $d c d m n r s T $.
    $d c d m n r s X $.
    uncmp.1 $e |- X = U. J $.
    $( The union of two compact sets is compact.  (Contributed by Jeff Hankins,
       30-Jan-2010.) $)
    uncmp $p |- ( ( ( J e. Top /\ X = ( S u. T ) ) /\
                  ( ( subSp ` <. S , J >. ) e. Comp /\
                    ( subSp ` <. T , J >. ) e. Comp ) ) -> J e. Comp ) $=
      ( vc vd vn vs vm vr wcel wceq wa cuni cv cfn wrex wi wss ctop cun cop cfv
      csubsp ccmp cpw wral simpll wb ssun1 sseq2 mpbiri ad2antlr cmpsub syl2anc
      cin simprr syl5eq sseqtrd unieq sseq2d pweq ineq1d rexeqdv imbi12d rcla4v
      ad2antrl mpid sylbid ssun2 reeanv elin simplbi elpwi anim12i unss simprbi
      syl sylib syl2an jca vex elpw2 anbi1i bitr2i simpllr ssun3 ssun4 ad2antll
      unfi eqsstrd uniun eqcomi 3sstr3g adantr sstrd uniss eqssd eqeq2d rcla4ev
      exp32 rexlimdvv syl5bir syl2and impancom exp3a ralrimiv iscmp sylanbrc )
      CUALZDABUBZMZNZACUCUEUDUFLZBCUCUEUDUFLZNZNZXKCOZFPZOZMZXSGPZOZMZGXTUGZQUQ
      ZRZSZFCUGZUHCUFLXKXMXQUIXRYIFYJXRXTYJLZYBYHXNYKYBNZXQYHXNYLNZXOAHPZOZTZHY
      GRZXPBIPZOZTZIYGRZYHYMXOAJPZOZTZYPHUUBUGZQUQZRZSZJYJUHZYQYMXKADTZXOUUIUJX
      KXMYLUIZXMUUJXKYLXMUUJAXLTABUKDXLAULUMUNZACDJHEUOUPYMUUIAYATZYQYMADYAUULY
      MDXSYAEXNYKYBURUSZUTYKUUIUUMYQSZSXNYBUUHUUOJXTYJUUBXTMZUUDUUMUUGYQUUPUUCY
      AAUUBXTVAVBUUPYPHUUFYGUUPUUEYFQUUBXTVCVDVEVFVGVHVIVJYMXPBKPZOZTZYTIUUQUGZ
      QUQZRZSZKYJUHZUUAYMXKBDTZXPUVDUJUUKXMUVEXKYLXMUVEBXLTBAVKDXLBULUMUNZBCDKI
      EUOUPYMUVDBYATZUUAYMBDYAUVFUUNUTYKUVDUVGUUASZSXNYBUVCUVHKXTYJUUQXTMZUUSUV
      GUVBUUAUVIUURYABUUQXTVAVBUVIYTIUVAYGUVIUUTYFQUUQXTVCVDVEVFVGVHVIVJYQUUANY
      PYTNZIYGRHYGRYMYHYPYTHIYGYGVLYMUVJYHHIYGYGYMYNYGLZYRYGLZNZUVJYHYMUVMUVJNZ
      NZYNYRUBZYGLZXSUVPOZMZYHUVOUVPXTTZUVPQLZNZUVQUVOUVTUWAUVOYNXTTZYRXTTZNZUV
      TUVMUWEYMUVJUVKUWCUVLUWDUVKYNYFLZUWCUVKUWFYNQLZYNYFQVMZVNYNXTVOVSUVLYRYFL
      ZUWDUVLUWIYRQLZYRYFQVMZVNYRXTVOVSVPVHYNYRXTVQVTZUVMUWAYMUVJUVKUWGUWJUWAUV
      LUVKUWFUWGUWHVRUVLUWIUWJUWKVRYNYRWKWAVHWBUVQUVPYFLZUWANUWBUVPYFQVMUWMUVTU
      WAUVPXTFWCWDWEWFVTUVOXSUVRUVODYOYSUBZXSUVRUVODXLUWNXKXMYLUVNWGUVOAUWNTZBU
      WNTZNZXLUWNTUVJUWQYMUVMYPUWOYTUWPAYOYSWHBYSYOWIVPWJABUWNVQVTWLEUVRUWNYNYR
      WMWNWOUVOUVPCTUVRXSTUVOUVPXTCUWLYLXTCTZXNUVNYKUWRYBXTCVOWPUNWQUVPCWRVSWSY
      EUVSGUVPYGYCUVPMYDUVRXSYCUVPVAWTXAUPXBXCXDXEXFXGXHFGCXIXJ $.
      $( [30-Jan-2010] $)
  $}

  ${
    $d f x z S $.  $d f w x y z J $.  $d f w x y z X $.
    hauscmp.1 $e |- X = U. J $.
    ${
      $d f w x y z A $.  $d f x z O $.  $d f x ph $.
      hauscmplem.2 $e |- O = { y e. J |
        E. w e. J ( A e. w /\ w C_ ( X \ y ) ) } $.
      hauscmplem.3 $e |- ( ph -> ( ( J e. Haus /\ S C_ X /\
        ( subSp ` <. S , J >. ) e. Comp ) /\ S =/= (/) /\ A e. ( X \ S ) ) ) $.
      $( Lemma for ~ hauscmp . $)
      hauscmplem $p |- ( ph -> E. z e. J ( A e. z /\ z C_ ( X \ S ) ) ) $=
        ( vx wss wrex wcel wa c0 wceq syl cuni cpw cfn cin cdif w3a cha wne cop
        vf cv csubsp ccmp simp1d adantr simp2d sselda difss simp3d sseldi wn id
        cfv eldifn nelne2 syl2anr hausnei syl13anc simp1 rexlimivw wi simp2 a1i
        elssuni syl6sseqr simp3 incom eqeq1i reldisj syl5bb syl5ib jcad reximia
        reximi crab unieqi eleq2i elunirab bitri sylibr ex ssrdv ssrab2 eqsstri
        jca wral elpw2g mpbiri ctop haustop cmpsub biimp3a syl3anc unieq sseq2d
        wb pweq ineq1d rexeqdv imbi12d rcla4va syl2anc mpd wex elin elpwi sseld
        wf difeq2 anbi2d rexbidv elrab2 simprbi syl6 ralrimiv eleq2 sseq1 sylbi
        anbi12d 3ad2ant2 cint simpl syl2an ciin ad2antll eliin 3syl cvv eqsstrd
        ax-mp ac6sfi crn simpl1 simprl 3ad2ant1 ssn0 uni0 syl6eq necon3i dm0rn0
        frn cdm fdm eqeq1d syl5rbbr necon3bid biimpac wfo wfn ffn dffn4 fiinopn
        sylib fofi imp ralimi mpbird fnrnfv inteqd fvex dfiin2 syl6eqr eleqtrrd
        cab ssel adantl ral2imi 3imtr4g ciun iindif2 simpl3 uniiun sseq2i sscon
        vex sstrd rcla4ev syl12anc exlimdv 3exp rexlimdv ) AFMUKZUAZNZMHUBZUCUD
        ZOZECUKZPZUWRIFUEZNZQZCGOZAFHUAZNZUWQAMFUXDAUWLFPZUWLUXDPZAUXFQZUWLBUKZ
        PZEDUKZPZUXKIUXIUEZNZQZDGOZQZBGOZUXGUXHUXJUXLUXIUXKUDZRSZUFZDGOZBGOZUXR
        UXHGUGPZUWLIPEIPZUWLEUHZUYCAUYDUXFAUYDFINZFGUIULVCUMPZAUYDUYGUYHUFZFRUH
        ZEUWTPZLUNZUNZUOAFIUWLAUYDUYGUYHUYLUPZUQAUYEUXFAUWTIEIFURAUYIUYJUYKLUSZ
        UTUOUXFUXFEFPVAZUYFAUXFVBAUYKUYPUYOEIFVDTUWLEFVEVFUWLEDBGIJVGVHUYBUXQBG
        UYBUXJUXPUYAUXJDGUXJUXLUXTVIVJUYAUXODGUXKGPZUYAUXLUXNUYAUXLVKUYQUXJUXLU
        XTVLVMUYQUXKINZUYAUXNVKUYQUXKGUAIUXKGVNJVOUYAUXTUYRUXNUXJUXLUXTVPUXTUXK
        UXIUDZRSUYRUXNUXSUYSRUXIUXKVQVRUXKUXIIVSVTWATWBWCWOWDTUXGUWLUXPBGWEZUAZ
        PUXRUXDVUAUWLHUYTKWFWGUXPBUWLGWHWIWJWKWLAHGUBZPZFUWRUAZNZUWNMUWRUBZUCUD
        ZOZVKZCVUBWPZUXEUWQVKZAVUCHGNZHUYTGKUXPBGWMWNAUYDVUCVULXFUYMHGUGWQTWRAG
        WSPZUYGUYHVUJAUYDVUMUYMGWTTZUYNAUYDUYGUYHUYLUSVUMUYGUYHVUJFGICMJXAXBXCV
        UIVUKCHVUBUWRHSZVUEUXEVUHUWQVUOVUDUXDFUWRHXDXEVUOUWNMVUGUWPVUOVUFUWOUCU
        WRHXGXHXIXJXKXLXMAUWNUXCMUWPAUWLUWPPZUWNUXCAVUPUWNUFZUWLGUJUKZXRZEUWRVU
        RVCZPZVUTIUWRUEZNZQZCUWLWPZQZUJXNZUXCVUPAVVGUWNVUPUWLUWOPZUWLUCPZQVVGUW
        LUWOUCXOZVVIVVIUXLUXKVVBNZQZDGOZCUWLWPVVGVVHVVIVBVVHVVMCUWLVVHUWRUWLPUW
        RHPZVVMVVHUWLHUWRUWLHXPXQVVNUWRGPVVMUXPVVMBUWRGHUXIUWRSZUXOVVLDGVVOUXNV
        VKUXLVVOUXMVVBUXKUXIUWRIXSXEXTYAKYBYCYDYEVVLVVDCDUWLGUJUXKVUTSUXLVVAVVK
        VVCUXKVUTEYFUXKVUTVVBYGYIUUAVFYHYJVUQVVFUXCUJVUQVVFUXCVUQVVFQZVURUUBZYK
        ZGPZEVVRPZVVRUWTNZUXCVVPVUMVVQGNZVVQRUHZVVQUCPZVVSVVPAVUMAVUPUWNVVFUUCZ
        VUNTVVPVUSVWBVUQVUSVVEUUDZUWLGVURUUKTVUQUWLRUHZVUSVWCVVFVUQUWMRUHZVWGVU
        QUWNUYJVWHAVUPUWNVPAVUPUYJUWNAUYIUYJUYKLUPUUEFUWMUUFXLUWLRUWMRUWLRSZUWM
        RUARUWLRXDUUGUUHUUITZVUSVVEYLVUSVWGVWCVUSUWLRVVQRVVQRSVURUULZRSVUSVWIVU
        RUUJVUSVWKUWLRUWLGVURUUMUUNUUOUUPUUQYMVUQVVIUWLVVQVURUURZVWDVVFVUPAVVIU
        WNVUPVVHVVIVVJYCYJVUSVWLVVEVUSVURUWLUUSZVWLUWLGVURUUTZUWLVURUVAUVCUOUWL
        VVQVURUVDYMVUMVWBVWCVWDUFVVSVVQGUVBUVEVHVVPECUWLVUTYNZVVRVVPEVWOPZVVACU
        WLWPZVVEVWQVUQVUSVVDVVACUWLVVAVVCYLUVFYOVVPAUYKVWPVWQXFVWEUYOCEUWLVUTUW
        TYPYQUVGVVPVUSVWMVVRVWOSVWFVWNVWMVVRUXIVUTSCUWLOBUVNZYKVWOVWMVVQVWRCBUW
        LVURUVHUVICBUWLVUTUWRVURUVJUVKUVLYQZUVMVVPVVRVWOUWTVWSVVPVWOCUWLVVBYNZU
        WTVVEVWOVWTNVUQVUSVVEBVWOVWTVVEUXIVUTPZCUWLWPZUXIVVBPZCUWLWPZUXIVWOPZUX
        IVWTPZVVDVXAVXCCUWLVVCVXAVXCVKVVAVUTVVBUXIUVOUVPUVQUXIYRPZVXEVXBXFBUWEZ
        CUXIUWLVUTYRYPYTVXGVXFVXDXFVXHCUXIUWLVVBYRYPYTUVRWLYOVVPVWTICUWLUWRUVSZ
        UEZUWTVVPVWGVWTVXJSVUQVWGVVFVWJUOCUWLIUWRUVTTVVPUWNVXJUWTNZAVUPUWNVVFUW
        AUWNFVXINVXKUWMVXIFCUWLUWBUWCFVXIIUWDYHTYSUWFYSUXBVVTVWAQCVVRGUWRVVRSUW
        SVVTUXAVWAUWRVVREYFUWRVVRUWTYGYIUWGUWHWKUWIXMUWJUWKXM $.
        $( [27-Nov-2013] $)
    $}

    $( A compact subspace of a T2 space is closed.  (Contributed by Jeff
       Hankins, 16-Jan-2010.)  (The proof was shortened by Mario Carneiro,
       14-Dec-2013.) $)
    hauscmp $p |- ( ( J e. Haus /\ S C_ X /\
                    ( subSp ` <. S , J >. ) e. Comp ) -> S e. ( Clsd ` J ) ) $=
      ( vx vz vy vw cha wcel wss cfv w3a c0 cdif cv wa wrex wb syl ccmp ccld wi
      cop csubsp ctop wceq haustop 3ad2ant1 0cld eleq1a 3syl wne wral crab eqid
      id hauscmplem ralrimdv eltop2 sylibrd simp2 biantrurd iscld bitr4d sylibd
      3exp pm2.61dne ) BIJZACKZABUDUELUAJZMZABUBLZJZANVLBUFJZNVMJANUGVNUCVIVJVO
      VKBUHUIZBUJNVMAUKULVLANUMZCAOZBJZVNVLVQEPZFPZJWAVRKQFBRZEVRUNZVSVLVQWBEVR
      VLVQVTVRJZWBVLVQWDMZGFHVTABVTHPZJWFCGPOKQHBRGBUOZCDWGUPWEUQURVGUSVLVOVSWC
      SVPEFVRBUTTVAVLVSVJVSQZVNVLVJVSVIVJVKVBVCVLVOVNWHSVPABCDVDTVEVFVH $.
      $( [27-Nov-2013] $) $( [16-Jan-2010] $)
  $}

  ${
    $d c r s u v w x y z J $.
    $( Lemma for ~ cmpfi . $)
    cmpfilem $p |- ( J e. Top -> ( A. x e. ~P ( Clsd ` J )
      ( A. w e. ( ~P x i^i Fin ) -. (/) = |^| w -> -. (/) = |^| x ) ->
        A. y e. ~P J ( A. z e. ( ~P y i^i Fin ) -. U. J = U. z ->
        -. U. J = U. y ) ) ) $=
      ( vv vu wcel c0 cv wceq wn cfn wi wa cdif wss adantr syl cvv vr ctop cint
      cpw cin wral ccld cfv cuni copab cima crn imassrn wf1o wfo opncldf1 f1ofo
      vs eqid forn 3syl syl5sseq fvex elpw2 sylibr ineq1d raleqdv eqeq2d notbid
      pweq inteq imbi12d rcla4v w3a elin vex elpw anbi1i ccnv imass2 adantl wf1
      bitri f1of1 f1imacnv sylan sylan2b sseqtrd 3adantl3 adantrr f1ocnv f1ofun
      wfun funimaex 3ad2ant1 elpwg mpbird simprr cen wbr cres sstr mpan2 sseq2d
      wb biimpa sylan2 3ad2antl1 f1ores syl2anc f1oen enfi mpbid sylanbrc unieq
      ciun wrex cab difeq2 ciin difeq2i ralrimivw simpl1 adantrlr eqcomd eqeq1d
      3eqtr3g syl5bb pm5.32d rexbidv2 abbidv cdm dfimafn eqtr4d 3eqtrd ex com23
      expr uni0 con3d ad2antll dif0 intiin iundif2 eqtr4i uniexg difexg dfiun2g
      eqcom ancoms sseld opncldf3 df-rn syl6sseq ad2antrl unieqd nsyld ralrimdv
      impr syl5bi difid uniiun wne syl6eq eqtr 3impia imp df-ne iindif2 dfiin2g
      syl5 ssel2 sylanb 3ad2antl2 adantrl opncldf2 f1ofn biimpar 3adant3 inteqd
      wfn fndm imim12d 3expia 0elpw mpbir2an eqcomi rcla4ev mp2an eqeq1 rexbidv
      0fin mpbiri dfrex2 sylib pm2.21d a1d pm2.61d2 syld ralrimdva ) EUBHZIDJZU
      CZKZLZDAJZUDZMUEZUFZIUXFUCZKZLZNZAEUGUHZUDZUFZEUIZCJZUIZKZLZCBJZUDZMUEZUF
      ZUXQUYBUIZKZLZNZBEUDZUXAUYBUYJHZOZUXPUXEDUAJZEHURJUXQUYMPKOUAURUJZUYBUKZU
      DZMUEZUFZIUYOUCZKZLZNZUYIUYLUYOUXOHZUXPVUBNUXAVUCUYKUXAUYOUXNQZVUCUXAUYNU
      LZUYOUXNUYNUYBUMZUXAEUXNUYNUNZEUXNUYNUOVUEUXNKURUAUYNEUXQUXQUSZUYNUSZUPZE
      UXNUYNUQEUXNUYNUTVAZVBZUYOUXNEUGVCVDVERUXMVUBAUYOUXOUXFUYOKZUXIUYRUXLVUAV
      UMUXEDUXHUYQVUMUXGUYPMUXFUYOVJVFVGVUMUXKUYTVUMUXJUYSIUXFUYOVKVHVIVLVMSUYL
      UXQIKZVUBUYINZUXAUYKVUNLZVUOUXAUYKVUPVNZUYEUYRVUAUYHVUQUYEUXEDUYQVUQUXBUY
      QHZUYEUXEVURUXBUYOQZUXBMHZOZVUQUYEUXENZVURUXBUYPHZVUTOVVAUXBUYPMVOVVCVUSV
      UTUXBUYODVPZVQVRWCVUQVVAVVBVUQVVAOZUYEUXQUYNVSZUXBUKZUIZKZUXDVVEVVGUYDHZU
      YEVVILZNVVEVVGUYCHZVVGMHZVVJVVEVVLVVGUYBQZVUQVUSVVNVUTUXAUYKVUSVVNVUPUYLV
      USOVVGVVFUYOUKZUYBVUSVVGVVOQUYLUXBUYOVVFVTWAUYLVVOUYBKZVUSUYKUXAUYBEQZVVP
      UYBEBVPVQZUXAEUXNUYNWBZVVQVVPUXAVUGVVSVUJEUXNUYNWDSEUXNUYBUYNWEWFWGRWHWIW
      JVVEVVGTHZVVLVVNXEVUQVVTVVAUXAUYKVVTVUPUXAVUGUXNEVVFUNZVVTVUJEUXNUYNWKZVW
      AVVFWMZVVTUXNEVVFWLZVVFUXBVVDWNSVAWORZVVGUYBTWPSWQVVEVUTVVMVUQVUSVUTWRVVE
      VVTUXBVVGWSWTZVUTVVMXEVWEVVEUXBVVGVVFUXBXAZUNZVWFVVEUXNEVVFWBZUXBUXNQZVWH
      VUQVWIVVAUXAUYKVWIVUPUXAVUGVWAVWIVUJVWBUXNEVVFWDVAWORVUQVUSVWJVUTUXAUYKVU
      SVWJVUPVUSUXAUXBVUEQZVWJVUSUYOVUEQVWKVUFUXBUYOVUEXBXCZUXAVWKVWJUXAVUEUXNU
      XBVUKXDXFXGXHWJUXNEUXBVVFXIXJUXBVVGVWGVVDXKSUXBVVGTXLXJXMVVGUYCMVOXNUYAVV
      KCVVGUYDUXRVVGKZUXTVVIVWMUXSVVHUXQUXRVVGXOVHVIVMSVUQVVAUXDVVIVUQVVAUXDOZO
      ZUXQFUXBUXQFJZPZXPZGJZVWQKZFUXBXQZGXRZUIZVVHVWOUXQIPZUXQUXCPZUXQVWRUXDVXD
      VXEKVUQVVAIUXCUXQXSUUAUXQUUBVXEUXQFUXBVWPXTZPVWRUXCVXFUXQFUXBUUCYAFUXBUXQ
      VWPUUDUUEYGVWOVWQTHZFUXBUFVWRVXCKVWOVXGFUXBVUQVXGVWNUXAUYKVXGVUPUXAUXQTHV
      XGEUBUUFUXQVWPTUUGSWOZRYBFGUXBVWQTUUHSVWOVXBVVGVWOVXBVWPVVFUHZVWSKZFUXBXQ
      ZGXRZVVGVWOVXAVXKGVWOVWTVXJFUXBUXBVWOVWPUXBHZVWTVXJVUQVWNVXMVWTVXJXEVWTVW
      QVWSKZVUQVWNVXMOZOZVXJVWSVWQUUIZVXPVWQVXIVWSVXPVXIVWQVXPUXAVWPUXNHZVXIVWQ
      KUXAUYKVUPVXOYCUXAUYKVXOVXRVUPUXAVVAVXMVXRUXDUXAVUSVXMVXRVUTUXAVUSVXMVXRU
      XAVUSOUXBUXNVWPUXAVUDVUSVWJVULVUSVUDVWJUXBUYOUXNXBUUJWFUUKUUSYDYDXHURUAVW
      PUYNEUXQVUHVUIUULXJYEYFYHYRYIYJYKVWOVWCUXBVVFYLZQZVVGVXLKVUQVWCVWNUXAUYKV
      WCVUPUXAVUGVWAVWCVUJVWBVWDVAWORVVAVXTVUQUXDVUSVXTVUTVUSUXBVUEVXSVWLUYNUUM
      UUNRUUOFGUXBVVFYMXJYNUUPYOYRUUQYPUUTYQUURVUQUYGUYTVUQUYGUYTVUQUYGOZIFUYBV
      WQXTZVWTFUYBXQZGXRZUCZUYSVYAIUXQFUYBVWPXPZPZVYBVYAUXQUXQPZUXQUYFPZIVYGUYG
      VYHVYIKVUQUXQUYFUXQXSWAUXQUVAUYFVYFUXQFUYBUVBYAYGVYAUYBIUVCZVYBVYGKVYAUYB
      IKZLZVYJVUQUYGVYLUXAUYKVUPUYGVYLNUYLUYGVUPVYLUYLUYGVUPVYLNUYLUYGOZVYKVUNV
      YKUYFIKZVYMVUNVYKUYFIUIZIUYBIXOYSUVDUYGVYNVUNNUYLUYGVYNVUNUXQUYFIUVEYPWAU
      VKYTYPYQUVFUVGUYBIUVHVEFUYBUXQVWPUVISYNVYAVXGFUYBUFVYBVYEKVYAVXGFUYBVUQVX
      GUYGVXHRYBFGUYBVWQTUVJSVYAVYDUYOVYAVYDVWPUYNUHZVWSKZFUYBXQZGXRZUYOVYAVYCV
      YRGVYAVWTVYQFUYBUYBVYAVWPUYBHZVWTVYQVUQUYGVYTVWTVYQXEVWTVXNVUQUYGVYTOZOZV
      YQVXQWUBVWQVYPVWSWUBVYPVWQWUBUXAVWPEHZVYPVWQKUXAUYKVUPWUAYCVUQVYTWUCUYGUY
      KUXAVYTWUCVUPUYKVVQVYTWUCVVRUYBEVWPUVLUVMUVNUVOURUAVWPUYNEUXQVUHVUIUVPXJY
      EYFYHYRYIYJYKVYAUYNWMZUYBUYNYLZQZUYOVYSKVUQWUDUYGUXAUYKWUDVUPUXAVUGWUDVUJ
      EUXNUYNWLSWORVUQWUFUYGUXAUYKWUFVUPUYKUXAVVQWUFVVRUXAWUFVVQUXAWUEEUYBUXAVU
      GUYNEUWAWUEEKVUJEUXNUYNUVQEUYNUWBVAXDUVRWGUVSRFGUYBUYNYMXJYNUVTYOYPYTUWCU
      WDVUNUYIVUBVUNUYEUYHVUNUXTCUYDXQZUYELVUNWUGIUXSKZCUYDXQZIUYDHZIVYOKZWUIWU
      JIUYCHIMHIUYCMVOUYBUWEUWLUWFVYOIYSUWGWUHWUKCIUYDUXRIKUXSVYOIUXRIXOVHUWHUW
      IVUNUXTWUHCUYDUXQIUXSUWJUWKUWMUXTCUYDUWNUWOUWPUWQUWRUWSUWT $.
      $( [28-Aug-2009] $)

    $( If a topology is compact and a collection of closed sets has the finite
       intersection property, its intersection is nonempty.  (Contributed by
       Jeff Hankins, 25-Aug-2009.) $)
    cmpfi $p |- ( J e. Top -> ( J e. Comp <-> A. x e. ~P ( Clsd ` J )
                  ( (/) e/ ( fi ` x ) -> |^| x =/= (/) ) ) ) $=
      ( vy vz vw vu vv wcel cv wceq cfn wi wral wn c0 wa cdif wss syl cvv vr vs
      vc ctop ccmp cuni cpw cin wrex cfi cfv wnel cint wne ccld iscmp wb con34b
      baib ralbii a1i copab ccnv cima cdm cnvimass wf1o wfn eqid opncldf1 f1ofn
      fndm 3syl syl5sseq elpw2g mpbird adantr pweq ineq1d raleqdv eqeq2d notbid
      unieq imbi12d rcla4v vex elpw wo pm2.1 neq0 ssel cldss sseq2 biimpd eleq1
      wex ss0 biimpa csn snelpwi snfi jctir elin sylibr 0ex intsn inteq rcla4ev
      eqcomi sylancl sylib ex com23 df-ne w3a f1ofun adantrr 3ad2antl1 3ad2ant1
      wfun wbr sseq2d syl2anc cab ciun difeq2 difeq2i 3eqtr3g adantrl ralrimivw
      ciin eqcomd syl6bb eqeq1 bitr2i rexbidv2 abbidv expr eqcom ralnex syl6com
      dfrex2 com13 sylcom com3l imp4c exlimiv sylbi vn0 mpbi int0 eqeq1i biimpi
      com12 eqcoms mtbiri orim12d mpii pm4.62 syl6ibr sylan2b a1dd anbi1i bitri
      mto funimass2 sylan funimaex elpwg simprr cen cres wf1 f1of1 mpan2 sylan2
      sstr f1ores f1oen enfi mpbid sylanbrc difid uniiun simpr uni0 syl6eq eqtr
      expcom con3rr3 mt2d 3ad2antl3 iindif2 uniexg difexg dfiin2g 3eqtr2d df-ss
      ad2antll eleq2d anbi1d anass df-br anbi12d anbi2d opelopab anbi2i syl6eqr
      cop dfima2 inteqd eqtrd syl5bi ralrimdv adantl dif0 intiin iundif2 eqtr4i
      nsyld dfiun2g simpl1 ssel2 3ad2antl2 eqeq1d syl5bb pm5.32d f1ocnv crn wfo
      opncldf3 f1ofo forn biimpar syl6sseq 3adant3 dfimafn eqtr4d unieqd 3eqtrd
      df-rn syl3an2b con3d imim12d 3expia syld ralrimdva cmpfilem impbid imbi1i
      pm2.61d fival ax-mp eleq2i rexbidv elab notbii 3bitr4i necon3bbii imbi12i
      df-nel 3bitr3g 3bitrd ) BUDHZBUEHZBUFZCIZUFZJZVVFDIZUFZJZDVVGUGZKUHZUIZLZ
      CBUGZMZVVONZVVINZLZCVVQMZOAIZUJUKZULZVWCUMZOUNZLZABUOUKZUGZMZVVEVVDVVRCDB
      UPUSVVRVWBUQVVDVVPVWACVVQVVIVVOURUTVAVVDVVLNZDVVNMZVVTLZCVVQMZOEIZUMZJZNZ
      EVWCUGZKUHZMZOVWFJZNZLZAVWJMZVWBVWKVVDVWOVXFVVDVWOVXEAVWJVVDVWCVWJHZPZVWO
      VWLDUAIZBHZUBIZVVFVXIQZJZPZUAUBVBZVCZVWCVDZUGZKUHZMZVVFVXQUFZJZNZLZVXEVXH
      VXQVVQHZVWOVYDLVVDVYEVXGVVDVYEVXQBRVVDVXOVEZVXQBVXOVWCVFZVVDBVWIVXOVGZVXO
      BVHVYFBJUBUAVXOBVVFVVFVIZVXOVIZVJZBVWIVXOVKBVXOVLVMZVNVXQBUDVOVPVQVWNVYDC
      VXQVVQVVGVXQJZVWMVXTVVTVYCVYMVWLDVVNVXSVYMVVMVXRKVVGVXQVRVSVTVYMVVIVYBVYM
      VVHVYAVVFVVGVXQWCWAWBWDWESVXHVVFOJZVYDVXELZVXHVYNVXEVYDVXGVVDVWCVWIRZVYNV
      XELVWCVWIAWFZWGZVVDVYPPZVYNVXBNZVXDWHZVXEVYSVYNVWCOJZNZWUBWHZWUAWUBWIVYSV
      YNWUDWUALVYSVYNPZWUCVYTWUBVXDWUCWUEVYTWUCUCIZVWCHZUCWPWUEVYTLZUCVWCWJWUGW
      UHUCWUGVVDVYPVYNVYTVYPWUGVVDVYNVYTLZVYPWUGWUFVWIHZVVDWUILVWCVWIWUFWKVVDWU
      JWUGWUIVVDWUJWUGWUILZVVDWUJPWUFVVFRZWUKWUFBVVFVYIWLWULVYNWUGVYTVYNWULWUFO
      RZWUGVYTLZVYNWULWUMVVFOWUFWMWNWUMWUFOJZWUNWUFWQWUOWUGVYTWUOWUGPZVWREVXAUI
      ZVYTWUPOVWCHZWUQWUOWUGWURWUFOVWCWOWRWUROWSZVXAHZOWUSUMZJZWUQWURWUSVWTHZWU
      SKHZPWUTWURWVCWVDOVWCWTOXAXBWUSVWTKXCXDWVAOOXEXFXIVWRWVBEWUSVXAVWPWUSJVWQ
      WVAOVWPWUSXGWAXHXJSVWREVXAUUBXKXLSUUAXMSXLUUCUUDUUEUUFUUGUUHUUNWUBVXDLWUE
      WUBVXCOOUMZJZWVFTOJZTOUNWVGNUUITOXNUUJWVGWVEOWVEOJWVGWVETOUUKUULUUMUUOUVE
      WUBVWFWVEOVWCOXGWAUUPVAUUQXLUURVXBVXCUUSUUTUVAUVBVVDVXGVYNNZVYOVVDVXGWVHX
      OZVXBVXTVYCVXDWVIVXBVWLDVXSWVIVVJVXSHZVXBVWLWVJVVJVXQRZVVJKHZPZWVIVXBVWLL
      ZWVJVVJVXRHZWVLPWVMVVJVXRKXCWVOWVKWVLVVJVXQDWFZWGUVCUVDWVIWVMWVNWVIWVMPZV
      XBOVXOVVJVDZUMZJZVVLWVQWVRVXAHZVXBWVTNZLWVQWVRVWTHZWVRKHZWWAWVQWWCWVRVWCR
      ZVVDVXGWVMWWEWVHVVDWVKWWEWVLVVDVXOXTZWVKWWEVVDVYHWWFVYKBVWIVXOXPZSVVJVWCV
      XOUVFUVGXQXRWVQWVRTHZWWCWWEUQWVIWWHWVMVVDVXGWWHWVHVVDVYHWWFWWHVYKWWGVXOVV
      JWVPUVHVMXSVQZWVRVWCTUVISVPWVQWVLWWDWVIWVKWVLUVJWVQWWHVVJWVRUVKYAZWVLWWDU
      QWWIWVQVVJWVRVXOVVJUVLZVGZWWJWVQBVWIVXOUVMZVVJBRZWWLWVIWWMWVMVVDVXGWWMWVH
      VVDVYHWWMVYKBVWIVXOUVNSXSVQVVDVXGWVMWWNWVHVVDWVKWWNWVLWVKVVDVVJVYFRZWWNWV
      KVXQVYFRWWOVYGVVJVXQVYFUVQUVOVVDWWOWWNVVDVYFBVVJVYLYBWRUVPXQZXRBVWIVVJVXO
      UVRYCVVJWVRWWKWVPUVSSVVJWVRTUVTYCUWAWVRVWTKXCUWBVWSWWBEWVRVXAVWPWVRJZVWRW
      VTWWQVWQWVSOVWPWVRXGWAWBWESWVIWVMVVLWVTWVIWVMVVLPZPZOFIZVVFGIZQZJZGVVJUIZ
      FYDZUMZWVSWWSOVVFGVVJWXAYEZQZGVVJWXBYKZWXFWWSVVFVVFQZVVFVVKQZOWXHVVLWXJWX
      KJWVIWVMVVFVVKVVFYFUWSVVFUWCVVKWXGVVFGVVJUWDYGYHWWSVVJOUNZWXIWXHJWWSVVJOJ
      ZNZWXLWVHVVDWWRWXNVXGWVHVVLWXNWVMWVHVVLPWXMVVLWVHVVLUWEWVHWXMVWLLVVLWXMVV
      LVYNWXMVVKOJZVVLVYNLWXMVVKOUFOVVJOWCUWFUWGVVLWXOVYNVVFVVKOUWHUWISUWJVQUWK
      YIUWLVVJOXNXDGVVJVVFWXAUWMSWWSWXBTHZGVVJMWXIWXFJWWSWXPGVVJWVIWXPWWRVVDVXG
      WXPWVHVVDVVFTHWXPBUDUWNVVFWXATUWOSZXSVQYJGFVVJWXBTUWPSUWQWWSWXEWVRWWSWXEW
      XAWWTVXOYAZGVVJUIZFYDWVRWWSWXDWXSFWWSWXCWXRGVVJVVJWWSWXAVVJHZWXCPZWXTWXAB
      HZWXCPZPZWXTWXRPWWSWYAWXTWYBPZWXCPWYDWWSWXTWYEWXCWWSWXTWXAVVJBUHZHWYEWWSV
      VJWYFWXAWWSWYFVVJWWSWWNWYFVVJJVVDVXGWWRWWNWVHVVDWVMWWNVVLWWPXQXRVVJBUWRXK
      YLUWTWXAVVJBXCYMUXAWXTWYBWXCUXBYMWYCWXRWXTWXRWXAWWTUXIVXOHWYCWXAWWTVXOUXC
      VXNWYBVXKWXBJZPWYCUAUBWXAWWTGWFFWFVXIWXAJZVXJWYBVXMWYGVXIWXABWOWYHVXLWXBV
      XKVXIWXAVVFYFWAUXDVXKWWTJWYGWXCWYBVXKWWTWXBYNUXEUXFYOUXGYMYPYQGFVXOVVJUXJ
      UXHUXKUXLYRUXTXLUXMXMUXNWVIVXCVYBVXGVVDVYPWVHVXCVYBLVYRVVDVYPWVHXOZVXCVYB
      WYIVXCPZVVFGVWCWXBYEZWXCGVWCUIZFYDZUFZVYAWYJVVFOQZVVFVWFQZVVFWYKVXCWYOWYP
      JWYIOVWFVVFYFUXOVVFUXPWYPVVFGVWCWXAYKZQWYKVWFWYQVVFGVWCUXQYGGVWCVVFWXAUXR
      UXSYHWYJWXPGVWCMWYKWYNJWYJWXPGVWCWYIWXPVXCVVDVYPWXPWVHWXQXSVQYJGFVWCWXBTU
      YASWYJWYMVXQWYJWYMWXAVXPUKZWWTJZGVWCUIZFYDZVXQWYJWYLWYTFWYJWXCWYSGVWCVWCW
      YJWXAVWCHZWXCWYSWYIVXCXUBWXCWYSUQWXCWXBWWTJWYIVXCXUBPZPZWYSWWTWXBYSXUDWXB
      WYRWWTXUDWYRWXBXUDVVDWXAVWIHZWYRWXBJVVDVYPWVHXUCUYBVYPVVDXUCXUEWVHVYPXUBX
      UEVXCVWCVWIWXAUYCYIUYDUBUAWXAVXOBVVFVYIVYJUYKYCYLUYEUYFYRUYGYPYQWYJVXPXTZ
      VWCVXPVEZRZVXQXUAJWYIXUFVXCVVDVYPXUFWVHVVDVYHVWIBVXPVGXUFVYKBVWIVXOUYHVWI
      BVXPXPVMXSVQWYIXUHVXCVVDVYPXUHWVHVYSVWCVXOUYIZXUGVVDVWCXUIRVYPVVDXUIVWIVW
      CVVDVYHBVWIVXOUYJXUIVWIJVYKBVWIVXOUYLBVWIVXOUYMVMYBUYNVXOVUAUYOUYPVQGFVWC
      VXPUYQYCUYRUYSUYTXLVUBVUCVUDVUEVUKVUFVUGACDEBVUHVUIVWNVWACVVQVWMVVSVVTVVL
      DVVNYTVUJUTVXEVWHAVWJVXBVWEVXDVWGWUQNOVWDHZNVXBVWEWUQXUJXUJOVVJVWQJZEVXAU
      IZDYDZHWUQVWDXUMOVWCTHVWDXUMJVYQEDVWCTVULVUMVUNXULWUQDOXEWXMXUKVWREVXAVVJ
      OVWQYNVUOVUPYOVUQVWREVXAYTOVWDVVAVURVXCVWFOOVWFYSVUSVUTUTVVBVVC $.
      $( [25-Nov-2013] $) $( [25-Aug-2009] $)
  $}


