$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Topological spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols and define their syntactical properties. $)

  $c Top $.
  $c TopSpOLD $.
  $c TopSp $.
  $c TopOpen $.
  $c TopBases $.
  $c topGen $.

  $( Extend class notation with the class of all topologies. $)
  ctop $a class Top $.

  $( Extend class notation with the class of all topological spaces. $)
  ctpsOLD $a class TopSpOLD $.

  $( Extend class notation with the topology component of a topological
     space. $)
  cts $a class TopOpen $.

  $( Extend class notation with the class of all topological spaces. $)
  ctps $a class TopSp $.

  $( Extend class notation with the class of all topological bases. $)
  ctb $a class TopBases $.

  $( Extend class notation with a function that converts a basis to its
     corresponding topology. $)
  ctg $a class topGen $.

  ${
    $d x y z w v $.
    $( Define the (proper) class of all topologies.  See ~ istop2g for an
       alternate way to express finite intersection and ~ istps5OLD for a
       standard definition in terms of both members of a topological space. $)
    df-top $a |- Top = { x | ( A. y e. ~P x U. y e. x /\
                             A. y e. x A. z e. x ( y i^i z ) e. x ) } $.

    $( Define the class of all topological spaces, each of which is an ordered
       pair the second of which is a topology on the first.  See ~ istps5OLD
       for a standard way to express a topological space. $)
    df-topspOLD $a |- TopSpOLD = { <. x , y >. | ( y e. Top /\ x = U. y ) } $.

    $( Define the class of all topological bases.  Equivalent to definition of
       basis in [Munkres] p. 78 (see ~ isbasis2g ).  Note that "bases" is the
       plural of "basis." $)
    df-bases $a |- TopBases = { x | A. y e. x A. z e. x ( y i^i z ) C_
                      U. ( x i^i ~P ( y i^i z ) ) } $.

    $( Define a function that converts a basis to its corresponding topology.
       Equivalent to the definition of a topology generated by a basis in
       [Munkres] p. 78 (see ~ tgval2 ).  See ~ tgval3 for an alternate
       expression for the value. $)
    df-topgen $a |- topGen = ( x e. TopBases |->
                               { y | y C_ U. ( x i^i ~P y ) } ) $.
  $}

  $( Define the topology component of a topological space (structure). $)
  df-topset $a |- TopOpen = ( f e. _V |-> ( f ` 9 ) ) $.

  $( Utility theorem: index-independent form of ~ df-topset . $)
  topsetid $p |- TopOpen = ( f e. _V |-> ( f ` ( TopOpen ` ndx ) ) ) $=
    ( cts c9 df-topset 9nn ndxid ) BCAADEF $.
    $( [20-Oct-2012] $)

  $( Utility lemma for constructing topological spaces without direct reference
     to the actual component indices. $)
  tpslem $p |- -. ( TopOpen ` ndx ) e. { ( Base ` ndx ) } $=
    ( vs cnx cts cfv cbs csn wcel wceq wne wn df-topset 9nn ndxarg 1re 9re 1lt9
    c9 c1 ltneii basendx neeqtrri eqnetri df-ne mpbi elsni mto ) BCDZBEDZFGUGUH
    HZUGUHIUIJUGQUHCQAAKLMQRUHRQNOPSTUAUBUGUHUCUDUGUHUEUF $.
    $( [2-Oct-2013] $)

  ${
    tpsfn.k $e |- K
         = { <. ( Base ` ndx ) , B >. , <. ( TopOpen ` ndx ) , J >. } $.
    ${
      tpsfn.d $e |- D = { ( Base ` ndx ) , ( TopOpen ` ndx ) } $.
      $( A constructed topology is a function. $)
      tpsfn $p |- K Fn D $=
        ( cnx cbs cfv csn cop cts fvex fnsn cpr cun df-pr eqtri tpslem fnunsn )
        GHIZJZBUAAKZJZDGLIZCGLMUAAGHMNDUCUECKZOUDUFJPEUCUFQRBUAUEOUBUEJPFUAUEQR
        ST $.
        $( [28-Sep-2013] $)
    $}

    $( The base set of a constructed topology. $)
    tpsbase $p |- ( B e. V -> B = ( Base ` K ) ) $=
      ( vs cbs cnx cfv cop cts cpr cvv prex eqeltri wfn wfun eqid tpsfn fnfun
      ax-mp baseid opex prid1 eleqtrri strfv ) ACGDFCHGIZAJZHKIZBJZLZMEUHUJNOCU
      GUILZPCQAULBCEULRSULCTUAFUBUHUKCUHUJUGAUCUDEUEUF $.
      $( [2-Oct-2013] $)

    $( The open sets of a constructed topology. $)
    tpstopset $p |- ( J e. V -> J = ( TopOpen ` K ) ) $=
      ( vs cts cnx cbs cfv cop cpr cvv prex eqeltri wfn wfun eqid tpsfn fnfun
      ax-mp topsetid opex prid2 eleqtrri strfv ) BCGDFCHIJZAKZHGJZBKZLZMEUHUJNO
      CUGUILZPCQAULBCEULRSULCTUAFUBUJUKCUHUJUIBUCUDEUEUF $.
      $( [2-Oct-2013] $)
  $}

  $( Define the class of all topological spaces (structures). $)
  df-topsp $a |- TopSp = { f | ( ( TopOpen ` f ) e. Top
      /\ ( Base ` f ) = U. ( TopOpen ` f ) ) } $.

  ${
    $d x y z J $.  $d x A $.
    $( Express the predicate " ` J ` is a topology."  Note:  In the literature,
       a topology is often represented by a script letter T, which resembles
       the letter J. This confusion may have led to J being used by some
       authors - e.g.  K. D. Joshi, _Introduction to General Topology_ (1983),
       p. 114 - and it is convenient for us since we later use ` T ` to
       represent linear transformations (operators).  (Contributed by Stefan
       Allan, 3-Mar-2006.) $)
    istopg $p |- ( J e. A -> ( J e. Top <-> ( A. x ( x C_ J -> U. x e. J )
                  /\ A. x e. J A. y e. J ( x i^i y ) e. J ) ) ) $=
      ( vz wcel ctop cv cuni cpw wral cin wa wss wal wceq pweq eleq2 raleqbi1dv
      wi anbi12d df-top elab2g df-ral elpw2g imbi1d albidv syl5bb anbi1d bitrd
      raleqbidv ) DCFZDGFAHZIZDFZADJZKZUMBHLZDFZBDKZADKZMZUMDNZUOTZAOZVAMUNEHZF
      ZAVFJZKZURVFFZBVFKZAVFKZMVBEDGCVFDPZVIUQVLVAVMVGUOAVHUPVFDQVFDUNRUKVKUTAV
      FDVJUSBVFDVFDURRSSUAEABUBUCULUQVEVAUQUMUPFZUOTZAOULVEUOAUPUDULVOVDAULVNVC
      UOUMDCUEUFUGUHUIUJ $.
      $( [11-Nov-2013] $) $( [3-Mar-2006] $)

    $( Express the predicate " ` J ` is a topology," using "the intersection of
       the elements of any finite subcollection" instead of the intersection of
       any two elements. $)
    istop2g $p |- ( J e. A -> ( J e. Top <-> ( A. x ( x C_ J -> U. x e. J )
         /\ A. x ( ( x C_ J /\ x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( vy wcel ctop cv wss cuni wi wal cin wral wa c0 wne cfn w3a cint istopg
      fiint anbi2i syl6bb ) CBECFEAGZCHZUDICEJAKZUDDGLCEDCMACMZNUFUEUDOPUDQERUD
      SCEJAKZNADBCTUGUHUFADCUAUBUC $.
      $( [23-Dec-2008] $) $( [19-Jul-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y J $.
    $( The union of a subset of a topology is an open set.  (Contributed by
       Stefan Allan, 27-Feb-2006.) $)
    uniopn $p |- ( ( J e. Top /\ A C_ J ) -> U. A e. J ) $=
      ( vx vy ctop wcel wss cuni cv wi wal cin wral wa istopg ibi simpld elpw2g
      cpw biimpar wceq sseq1 unieq eleq1d imbi12d cla4gv syl com23 pm2.43d mpid
      ex imp ) BEFZABGZAHZBFZUMUNCIZBGZUQHZBFZJZCKZUPUMVBUQDILBFDBMCBMZUMVBVCNC
      DEBOPQUMUNVBUPJZUMUNUNVDJUMUNNZVBUNUPVEABSZFZVBUNUPJZJUMVGUNABERTVAVHCAVF
      UQAUAZURUNUTUPUQABUBVIUSUOBUQAUCUDUEUFUGUHUKUIUJUL $.
      $( [27-Feb-2006] $)

    $( The indexed union of a subset of a topology is an open set. $)
    iunopn $p |- ( ( J e. Top /\ A. x e. A B e. J ) -> U_ x e. A B e. J ) $=
      ( vy ctop wcel wral wa ciun cv wceq wrex cab dfiun2g adantl wss uniiunlem
      cuni ibi uniopn sylan2 eqeltrd ) DFGZCDGABHZIABCJZEKCLABMENZSZDUEUFUHLUDA
      EBCDOPUEUDUGDQZUHDGUEUIAEBCDDRTUGDUAUBUC $.
      $( [5-Oct-2006] $)

    $( The intersection of two open sets of a topology is also an open set. $)
    inopn $p |- ( ( J e. Top /\ A e. J /\ B e. J ) -> ( A i^i B ) e. J ) $=
      ( vx vy ctop wcel cin cv wral wa wss cuni wi wal istopg ibi simprd eleq1d
      wceq ineq1 ineq2 rcla42v syl5com 3impib ) CFGZACGZBCGZABHZCGZUFDIZEIZHZCG
      ZECJDCJZUGUHKUJUFUKCLUKMCGNDOZUOUFUPUOKDEFCPQRUNUJAULHZCGDEABCCUKATUMUQCU
      KAULUASULBTUQUICULBAUBSUCUDUE $.
      $( [17-Jul-2006] $)

    $( A topology is closed under finite intersections.  (Contributed by Jeff
       Hankins, 7-Oct-2009.) $)
    fitop $p |- ( J e. Top -> ( fi ` J ) = J ) $=
      ( vx vy ctop wcel cv cin wral cfi cfv inopn 3expib ralrimivv inficl mpbid
      wceq ) ADEZBFZCFZGAEZCAHBAHAIJAPQTBCAAQRAESAETRSAKLMBCADNO $.
      $( [7-Oct-2009] $)

    $( The intersection of a non-empty finite family of open sets is open.
       (Contributed by FL, 20-Apr-2012.) $)
    fiinopn $p |- ( J e. Top -> ( ( A C_ J /\ A =/= (/) /\ A e. Fin )
        -> |^| A e. J ) ) $=
      ( vx wss c0 wne cfn wcel w3a ctop cint wi elpwg cv wceq sseq1 neeq1 com12
      cpw wal eleq1 3anbi123d inteq eleq1d imbi2d imbi12d wa istop2g ibi adantl
      cuni ax4 syl vtoclg 3exp com3r com4r syl6bir pm2.43a com4l pm2.43i 3imp )
      ABDZAEFZAGHZIZBJHZAKZBHZVCVDVEVGVILZVCVDVEVJLLVEVCVCVDVJVCVEVCVDVJLLZVEVC
      ABSZHZVEVKLABGMVEVCVDVMVJVCVDVEVMVJLZVCVDVEVNVMVFVJCNZBDZVOEFZVOGHZIZVGVO
      KZBHZLZLVFVJLCAVLVOAOZVSVFWBVJWCVPVCVQVDVRVEVOABPVOAEQVOAGUAUBWCWAVIVGWCV
      TVHBVOAUCUDUEUFVGVSWAVGVPVOUKBHLCTZVSWALZCTZUGZWEVGWGCJBUHUIWFWEWDWECULUJ
      UMRUNRUOUPUQURUSUTVAVBR $.
      $( [20-Apr-2012] $)

    $( The intersection of a non-empty finite family of open sets is open.
       (Contributed by Mario Carneiro, 14-Sep-2014.) $)
    iinopn $p |- ( ( J e. Top /\ ( A e. Fin /\ A =/= (/) /\
      A. x e. A B e. J ) ) -> |^|_ x e. A B e. J ) $=
      ( vy ctop wcel cfn c0 wne wral w3a wa ciin cv wceq wrex syl wss sylib cab
      cint simpr3 dfiin2g simpl cmpt crn eqid rnmpt wf frn syl5eqssr cdm simpr2
      fmpt fdm eqnetrd dm0rn0 eqeq1i bitri necon3bii simpr1 abrexfi fiinopn imp
      syl13anc eqeltrd ) DFGZBHGZBIJZCDGABKZLZMZABCNZEOCPABQEUAZUBZDVMVKVNVPPVH
      VIVJVKUCZAEBCDUDRVMVHVODSZVOIJZVOHGZVPDGZVHVLUEVMVOABCUFZUGZDAEBCWBWBUHZU
      IZVMBDWBUJZWCDSVMVKWFVQABDCWBWDUOTZBDWBUKRULVMWBUMZIJVSVMWHBIVMWFWHBPWGBD
      WBUPRVHVIVJVKUNUQWHIVOIWHIPWCIPVOIPWBURWCVOIWEUSUTVATVMVIVTVHVIVJVKVBAEBC
      VCRVHVRVSVTLWAVODVDVEVFVG $.
      $( [14-Sep-2014] $)
  $}

  $( The union of two open sets is open.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  unopn $p |- ( ( J e. Top /\ A e. J /\ B e. J ) -> ( A u. B ) e. J ) $=
    ( ctop wcel w3a cpr cuni cun wceq uniprg 3adant1 wa wss prssg uniopn sylan2
    ibi 3impb eqeltrrd ) CDEZACEZBCEZFABGZHZABIZCUBUCUEUFJUAABCCKLUAUBUCUECEZUB
    UCMZUAUDCNZUGUHUIABCCCORUDCPQST $.
    $( [2-Sep-2009] $)

  $( The empty set is an open subset of a topology.  (Contributed by Stefan
     Allan, 27-Feb-2006.) $)
  0opn $p |- ( J e. Top -> (/) e. J ) $=
    ( ctop wcel c0 cuni uni0 wss 0ss uniopn mpan2 syl5eqelr ) ABCZDDEZAFLDAGMAC
    AHDAIJK $.
    $( [27-Feb-2006] $)

  $( The empty set is not a topology.  (Contributed by FL, 1-Jun-2008.) $)
  0ntop $p |- -. (/) e. Top $=
    ( c0 ctop wcel noel 0opn mto ) ABCAACADAEF $.
    $( [1-Jun-2008] $)

  ${
    1open.1 $e |- X = U. J $.
    $( The underlying set of a topology is an open set. $)
    topopn $p |- ( J e. Top -> X e. J ) $=
      ( ctop wcel cuni wss ssid uniopn mpan2 syl5eqel ) ADEZBAFZACLAAGMAEAHAAIJ
      K $.
      $( [17-Jul-2006] $)

    $( A member of a topology is a subset of its underlying set. $)
    eltopss $p |- ( ( J e. Top /\ A e. J ) -> A C_ X ) $=
      ( wcel wss ctop cuni elssuni syl6sseqr adantl ) ABEZACFBGELABHCABIDJK $.
      $( [12-Sep-2006] $)

    $( The topologies over the same set have a greatest element (the discrete
       topology) and a least element (the indiscrete topology).  (Contributed
       by FL, 18-Apr-2010.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    topgele $p |- ( J e. Top -> ( { (/) , X } C_ J /\ J C_ ~P X ) ) $=
      ( ctop wcel c0 cpr wss cpw wa 0opn topopn jca cvv 0ex prssg sylancr mpbid
      wb wceq cuni pwuni pweq ax-mp sseqtr4i jctir ) ADEZFBGAHZABIZHUGFAEZBAEZJ
      ZUHUGUJUKAKABCLZMUGFNEUKULUHSOUMFBANAPQRAAUAZIZUIAUBBUNTUIUOTCBUNUCUDUEUF
      $.
      $( [15-Dec-2013] $) $( [18-Apr-2010] $)

    $( The only topology on a singleton is the indiscrete topology.
       (Contributed by FL, 5-Jan-2009.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    topsn $p |- ( ( J e. Top /\ X = { A } ) -> J = { (/) , { A } } ) $=
      ( ctop wcel csn wceq wa cpr wss cpw topgele preq2 sseq1d pweq pwsn syl6eq
      c0 sseq2d anbi12d eqss syl6bbr syl5ibcom imp eqcomd ) BEFZCAGZHZISUHJZBUG
      UIUJBHZUGSCJZBKZBCLZKZIZUIUKBCDMUIUPUJBKZBUJKZIUKUIUMUQUOURUIULUJBCUHSNOU
      IUNUJBUIUNUHLUJCUHPAQRTUAUJBUBUCUDUEUF $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d x y A $.  $d x y J $.
    $( Construct a topological space from a topology and vice-versa.  We say
       that ` A ` is a topology on ` U. A ` .  (This could be proved more
       efficiently from ~ istpsOLD , but the proof here does not require the
       Axiom of Regularity.) $)
    eltopspOLD $p |- ( <. U. J , J >. e. TopSpOLD <-> J e. Top ) $=
      ( vy vx cuni cop ctpsOLD wcel cvv wa ctop wbr df-br wrel cv copab relopab
      wceq df-topspOLD releqi jca mpbir sylbir uniexb sylibr uniexg elex anbi2d
      brrelexi eqeq1 eleq1 eqeq2d anbi12d opelopabg eleq2i eqid biantru 3bitr4g
      unieq pm5.21nii ) ADZAEZFGZUTHGZAHGZIZAJGZVBVCVDVBUTAFKVCUTAFLUTAFFMBNZJG
      ZCNZVGDZQZIZCBOZMVLCBPFVMCBRZSUAUHUBZVBVCVDVOAUCUDTVFVCVDAJUEAJUFTVEVAVMG
      VFUTUTQZIZVBVFVLVHUTVJQZIVQCBUTAHHVIUTQVKVRVHVIUTVJUIUGVGAQZVHVFVRVPVGAJU
      JVSVJUTUTVGAURUKULUMFVMVAVNUNVPVFUTUOUPUQUS $.
      $( [8-Mar-2006] $)

    $( Existence implied by membership in a topological space.  This technical
       lemma, which can help eliminate unnecessary antecedents, uses the Axiom
       of Regularity (via ~ elirr ) along with definitional tricks. $)
    tpsexOLD $p |- ( <. A , J >. e. TopSpOLD -> ( A e. _V /\ J e. _V ) ) $=
      ( vy vx cop ctpsOLD wcel cvv wbr df-br wrel cv ctop cuni wceq wa brrelexi
      sylbir adantr jca copab relopab df-topspOLD releqi mpbir elirr simpr eqid
      wn topopn eqeltrd mto elex eleq2i eqeq1 anbi2d eleq1 unieq eqeq2d anbi12d
      opelopabg syl5bb pm5.21nii mtbir opprc2 eleq1d mtbiri con4i ) ABEZFGZAHGZ
      BHGZVJABFIVKABFJABFFKCLZMGZDLZVMNZOZPZDCUAZKVRDCUBFVSDCUCZUDUEZQRVLVJVLUI
      ZVJAAEZFGZWDAMGZAANZOZPZWHAAGAUFWHAWFAWEWGUGWEWFAGWGAWFWFUHUJSUKULWDVKVKP
      ZWHWDVKVKWDAAFIVKAAFJAAFWAQRZWJTWEWIWGWEVKVKAMUMZWKTSWDWCVSGWIWHFVSWCVTUN
      VRVNAVPOZPWHDCAAHHVOAOVQWLVNVOAVPUOUPVMAOZVNWEWLWGVMAMUQWMVPWFAVMAURUSUTV
      AVBVCVDWBVIWCFABVEVFVGVHT $.
      $( [18-Jul-2006] $)

    $( Express the predicate "is a topological space." $)
    istpsOLD $p |- ( <. A , J >. e. TopSpOLD <-> ( J e. Top /\ A = U. J ) ) $=
      ( vy vx cop ctpsOLD wcel cvv ctop cuni wceq tpsexOLD simpr uniexg eqeltrd
      wa adantr elex jca cv copab df-topspOLD eleq2i eqeq1 anbi2d eleq1 anbi12d
      unieq eqeq2d opelopabg syl5bb pm5.21nii ) ABEZFGZAHGZBHGZPZBIGZABJZKZPZAB
      LVAUOUPVAAUSHURUTMURUSHGUTBINQOURUPUTBIRQSUNUMCTZIGZDTZVBJZKZPZDCUAZGUQVA
      FVHUMDCUBUCVGVCAVEKZPVADCABHHVDAKVFVIVCVDAVEUDUEVBBKZVCURVIUTVBBIUFVJVEUS
      AVBBUHUIUGUJUKUL $.
      $( [18-Jul-2006] $)

    $( Express the predicate "is a topological space." $)
    istps2OLD $p |- ( <. A , J >. e. TopSpOLD <-> ( ( J e. Top /\ J C_ ~P A )
                  /\ ( (/) e. J /\ A e. J ) ) ) $=
      ( ctop wcel cuni wceq wa cpw wss c0 cop ctpsOLD eqimss adantl 0opn adantr
      simpr eqid topopn eqeltrrd jca32 ex unissel adantrl impbid1 eqcom sspwuni
      anbi1i 3bitr4g pm5.32i istpsOLD anass 3bitr4i ) BCDZABEZFZGUNBAHIZJBDZABD
      ZGZGZGABKLDUNUQGUTGUNUPVAUNUOAFZUOAIZUTGZUPVAUNVBVDUNVBVDUNVBGZVCURUSVBVC
      UNUOAMNUNURVBBOPVEUOABUNVBQUNUOBDVBBUOUORSPTUAUBVCUSVBURBAUCUDUEAUOUFUQVC
      UTBAUGUHUIUJABUKUNUQUTULUM $.
      $( [18-Jul-2006] $)

    $( A standard textbook definition of a topological space. $)
    istps3OLD $p |- ( <. A , J >. e. TopSpOLD <-> ( ( J C_ ~P A /\ (/) e. J /\
                 A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
                 A. x e. J A. y e. J ( x i^i y ) e. J ) ) ) $=
      ( cop ctpsOLD wcel ctop cpw wss wa c0 w3a cv cuni wi wal cin wral cvv syl
      istps2OLD anass ancom 3anass anbi1i wb pwexg ssexg sylan2 3adant2 pm5.32i
      istopg 3bitr2i 3bitri ) CDEFGDHGZDCIZJZKLDGZCDGZKZKUPURVAKZKZURUSUTMZANZD
      JVEODGPAQVEBNRDGBDSADSKZKZCDUBUPURVAUCVCVBUPKVDUPKVGUPVBUDVDVBUPURUSUTUEU
      FVDUPVFVDDTGZUPVFUGURUTVHUSUTURUQTGVHCDUHDUQTUIUJUKABTDUMUAULUNUO $.
      $( [18-Jul-2006] $)

    $( A standard textbook definition of a topological space. $)
    istps4OLD $p |- ( <. A , J >. e. TopSpOLD <-> ( ( J C_ ~P A /\ (/) e. J /\
       A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\ A. x ( ( x C_ J /\
       x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( vy cop ctpsOLD wcel cpw wss c0 w3a cv cuni wi wal cin wral wne anbi2i
      wa cfn cint istps3OLD fiint bitri ) BCEFGCBHIJCGBCGKZALZCIZUGMCGNAOZUGDLP
      CGDCQACQZTZTUFUIUHUGJRUGUAGKUGUBCGNAOZTZTADBCUCUKUMUFUJULUIADCUDSSUE $.
      $( [30-Dec-2008] $) $( [19-Jul-2006] $)

    $( A standard textbook definition of a topological space ` <. A , J >. ` :
       a topology on ` A ` is a collection ` J ` of subsets of ` A ` such that
       ` (/) ` and ` A ` are in ` J ` and that is closed under union and finite
       intersection.  Definition of topological space in [Munkres] p. 76. $)
    istps5OLD $p |- ( <. A , J >. e. TopSpOLD <-> ( ( A. x e. J x C_ A /\ (/)
        e. J
                 /\ A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
           A. x ( ( x C_ J /\ x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( cop ctpsOLD wcel cpw wss c0 w3a cv cuni wal wne cfn cint wral istps4OLD
      wi wa pwssb 3anbi1i anbi1i bitri ) BCDEFCBGHZICFZBCFZJZAKZCHZUILCFSAMUJUI
      INUIOFJUIPCFSAMTZTUIBHACQZUFUGJZUKTABCRUHUMUKUEULUFUGACBUAUBUCUD $.
      $( [23-Apr-2009] $) $( [19-Jul-2006] $)
  $}

  ${
    $d f A $.  $d f J $.  $d f K $.
    istps.a $e |- A = ( Base ` K ) $.
    istps.j $e |- J = ( TopOpen ` K ) $.
    $( Express the predicate "is a topological space." $)
    istps $p |- ( K e. TopSp <-> ( J e. Top /\ A = U. J ) ) $=
      ( vf ctps wcel cv cts cfv ctop cbs cuni wceq wa cab c0 eleq1d fveq2 0ntop
      df-topsp eleq2i cvv eleq1i fvprc mtbiri con4i sylbi adantr unieqd eqeq12d
      wn anbi12d unieqi eqeq12i anbi12i syl6bbr elab3 bitri ) CGHCFIZJKZLHZVAMK
      ZVBNZOZPZFQZHBLHZABNZOZPZGVHCFUBUCVGVLFCVICUDHZVKVICJKZLHZVMBVNLEUEZVMVOV
      MUMZVORLHUAVQVNRLCJUFSUGUHUIUJVACOZVGVOCMKZVNNZOZPVLVRVCVOVFWAVRVBVNLVACJ
      TZSVRVDVSVEVTVACMTVRVBVNWBUKULUNVIVOVKWAVPAVSVJVTDBVNEUOUPUQURUSUT $.
      $( [20-Oct-2012] $)

    $( Express the predicate "is a topological space." $)
    istps2 $p |- ( K e. TopSp <-> ( ( J e. Top /\ J C_ ~P A )
                  /\ ( (/) e. J /\ A e. J ) ) ) $=
      ( ctop wcel cuni wceq wa cpw c0 ctps eqimss adantl 0opn adantr simpr eqid
      wss topopn eqeltrrd jca32 ex unissel adantrl impbid1 eqcom sspwuni anbi1i
      3bitr4g pm5.32i istps anass 3bitr4i ) BFGZABHZIZJUPBAKTZLBGZABGZJZJZJCMGU
      PUSJVBJUPURVCUPUQAIZUQATZVBJZURVCUPVDVFUPVDVFUPVDJZVEUTVAVDVEUPUQANOUPUTV
      DBPQVGUQABUPVDRUPUQBGVDBUQUQSUAQUBUCUDVEVAVDUTBAUEUFUGAUQUHUSVEVBBAUIUJUK
      ULABCDEUMUPUSVBUNUO $.
      $( [20-Oct-2012] $) $( [18-Jul-2006] $)

    $d x y J $.
    $( A standard textbook definition of a topological space. $)
    istps3 $p |- ( K e. TopSp <-> ( ( J C_ ~P A /\ (/) e. J /\
                 A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
                 A. x e. J A. y e. J ( x i^i y ) e. J ) ) ) $=
      ( ctps wcel ctop cpw wss wa c0 w3a cv cuni wi wral cvv istps2 anass ancom
      wal cin 3anass anbi1i wb pwexg ssexg sylan2 3adant2 istopg pm5.32i 3bitri
      syl 3bitr2i ) EHIDJIZDCKZLZMNDIZCDIZMZMURUTVCMZMZUTVAVBOZAPZDLVGQDIRAUDVG
      BPUEDIBDSADSMZMZCDEFGUAURUTVCUBVEVDURMVFURMVIURVDUCVFVDURUTVAVBUFUGVFURVH
      VFDTIZURVHUHUTVBVJVAVBUTUSTIVJCDUIDUSTUJUKULABTDUMUPUNUQUO $.
      $( [20-Oct-2012] $) $( [18-Jul-2006] $)

    $( A standard textbook definition of a topological space. $)
    istps4 $p |- ( K e. TopSp <-> ( ( J C_ ~P A /\ (/) e. J /\
       A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\ A. x ( ( x C_ J /\
       x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( vy ctps wcel cpw wss c0 w3a cv cuni wi wal wral wa anbi2i cin wne fiint
      cfn cint istps3 bitri ) DHICBJKLCIBCIMZANZCKZUIOCIPAQZUIGNUACIGCRACRZSZSU
      HUKUJUILUBUIUDIMUIUECIPAQZSZSAGBCDEFUFUMUOUHULUNUKAGCUCTTUG $.
      $( [20-Oct-2012] $) $( [19-Jul-2006] $)

    $d x A $.
    $( A standard textbook definition of a topological space ` <. A , J >. ` :
       a topology on ` A ` is a collection ` J ` of subsets of ` A ` such that
       ` (/) ` and ` A ` are in ` J ` and that is closed under union and finite
       intersection.  Definition of topological space in [Munkres] p. 76. $)
    istps5 $p |- ( K e. TopSp <-> ( ( A. x e. J x C_ A /\ (/) e. J
                 /\ A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
           A. x ( ( x C_ J /\ x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( ctps wcel cpw wss c0 w3a cv cuni wi wal wne cfn cint wa istps4 3anbi1i
      wral pwssb anbi1i bitri ) DGHCBIJZKCHZBCHZLZAMZCJZUKNCHOAPULUKKQUKRHLUKSC
      HOAPTZTUKBJACUCZUHUILZUMTABCDEFUAUJUOUMUGUNUHUIACBUDUBUEUF $.
      $( [20-Oct-2012] $) $( [19-Jul-2006] $)
  $}

  ${
    istpsi.b $e |- ( Base ` K ) = A $.
    istpsi.j $e |- ( TopOpen ` K ) = J $.
    istpsi.1 $e |- A = U. J $.
    istpsi.2 $e |- J e. Top $.
    $( Properties that determine a topological space. $)
    istpsi $p |- K e. TopSp $=
      ( ctps wcel ctop cuni wceq cbs cfv eqcomi cts istps mpbir2an ) CHIBJIABKL
      ABCCMNADOCPNBEOQGFR $.
      $( [20-Oct-2012] $)
  $}

  ${
    top2tps.j $e |- J = ( TopOpen ` K ) $.
    top2tps.p $e |- ( J e. Top -> ph ) $.
    $( Convert a topology theorem to a topological space theorem.  The second
       hypothesis is any existing theorem about ` J ` . $)
    top2tps $p |- ( K e. TopSp -> ph ) $=
      ( ctps wcel ctop cbs cfv cuni wceq wa eqid istps adantr sylbi ) CFGBHGZCI
      JZBKLZMASBCSNDORATEPQ $.
      $( [23-Oct-2012] $)
  $}

  ${
    eltps.a $e |- A e. _V $.
    eltps.j $e |- J e. _V $.
    eltps.k $e |- K
         = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Express the predicate "is a topological space" using a constructed
       structure. $)
    eltps $p |- ( K e. TopSp <-> ( J e. Top /\ A = U. J ) ) $=
      ( cvv wcel cbs cfv wceq tpsbase ax-mp cts tpstopset istps ) ABCAGHACIJKDA
      BCGFLMBGHBCNJKEABCGFOMP $.
      $( [20-Oct-2012] $)
  $}

  ${
    $d a j A $.  $d j J $.
    eltpsg.k $e |- K
         = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Express the predicate "is a topological space" using a constructed
       structure. $)
    eltpsg $p |- ( ( A e. B /\ J e. C )
                  -> ( K e. TopSp <-> ( J e. Top /\ A = U. J ) ) ) $=
      ( va vj ctps wcel cnx cfv cop cpr wa ctop cuni wceq cv wb cbs opeq2 eqeq1
      cts eleq1i preq1d eleq1d anbi2d bibi12d preq2d eleq1 unieq eqeq2d anbi12d
      vex eqid eltps vtocl2g syl5bb ) EIJKUALZAMZKUDLZDMZNZIJZABJDCJODPJZADQZRZ
      OZEVDIFUEUTGSZMZVBHSZMZNZIJZVLPJZVJVLQZRZOZTVAVMNZIJZVPAVQRZOZTVEVITGHADB
      CVJARZVOWAVSWCWDVNVTIWDVKVAVMVJAUTUBUFUGWDVRWBVPVJAVQUCUHUIVLDRZWAVEWCVIW
      EVTVDIWEVMVCVAVLDVBUBUJUGWEVPVFWBVHVLDPUKWEVQVGAVLDULUMUNUIVJVLVNGUOHUOVN
      UPUQURUS $.
      $( [20-Oct-2012] $)
  $}

  ${
    eltpsi.k $e |- K
         = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , J >. } $.
    eltpsi.u $e |- A = U. J $.
    eltpsi.j $e |- J e. Top $.
    $( Properties that determine a topological space from a construction (using
       no explicit indices). $)
    eltpsi $p |- K e. TopSp $=
      ( ctps wcel ctop cuni wceq cvv elexi uniex eqeltri eltps mpbir2an ) CGHBI
      HABJZKABCARLEBBIFMZNOSDPFEQ $.
      $( [20-Oct-2012] $)
  $}

  ${
    eltopspg.k $e |- K
         = { <. ( Base ` ndx ) , U. J >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Construct a topological space from a topology and vice-versa.  We say
       that ` A ` is a topology on ` U. A ` . $)
    eltopspg $p |- ( J e. V -> ( K e. TopSp <-> J e. Top ) ) $=
      ( wcel ctps ctop cuni wceq cvv uniexg eltpsg mpancom eqid biantru syl6bbr
      wa wb ) ACEZBFEZAGEZAHZUBIZQZUAUBJESTUDRACKUBJCABDLMUCUAUBNOP $.
      $( [20-Oct-2012] $)
  $}

  ${
    eltopsp.j $e |- J e. _V $.
    eltopsp.k $e |- K
         = { <. ( Base ` ndx ) , U. J >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Construct a topological space from a topology and vice-versa.  We say
       that ` A ` is a topology on ` U. A ` . $)
    eltopsp $p |- ( K e. TopSp <-> J e. Top ) $=
      ( cvv wcel ctps ctop wb eltopspg ax-mp ) AEFBGFAHFICABEDJK $.
      $( [20-Oct-2012] $)
  $}

  ${
    tpsprop.a $e |- ( Base ` K ) = A $.
    tpsprop.j $e |- ( TopOpen ` K ) = J $.
    tpsprop.b $e |- ( Base ` L ) = A $.
    tpsprop.t $e |- ( TopOpen ` L ) = J $.
    $( Any set with the same (relevant) properties as a given topological space
       is also a topological space (and vice-versa). $)
    tpsprop $p |- ( K e. TopSp <-> L e. TopSp ) $=
      ( ctps wcel ctop cuni wceq wa cbs cfv eqcomi cts istps bitr4i ) CIJBKJABL
      MNDIJABCCOPAEQCRPBFQSABDDOPAGQDRPBHQST $.
      $( [28-Oct-2012] $)
  $}

  ${
    tpsstr.a $e |- ( Base ` K ) = A $.
    tpsstr.j $e |- ( TopOpen ` K ) = J $.
    tpsstr.l $e |- L
          = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Generalize a specific 2-element topological space ` L ` to show that any
       set ` K ` with the same (relevant) properties is also a topological
       space. $)
    tpsstr $p |- ( K e. TopSp <-> L e. TopSp ) $=
      ( ctps wcel ctop cuni wceq wa cbs cfv eqcomi cts cvv fvex eqeltrri bitr4i
      istps eltps ) CHIBJIABKLMDHIABCCNOZAEPCQOZBFPUBABDUDARECNSTUEBRFCQSTGUCUA
      $.
      $( [28-Oct-2012] $)
  $}

