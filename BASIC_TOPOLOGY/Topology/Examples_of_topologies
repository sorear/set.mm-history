$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Examples of topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c d x y z J $.  $d a b c d x y z Y $.
    $( A subspace topology is a topology.  Definition of subspace topology in
       [Munkres] p. 89. ` Y ` is normally a subset of the base set of ` J ` .
       (Contributed by FL, 15-Apr-2007.) $)
    subtop $p |- ( J e. Top -> { x | E. y e. J x = ( y i^i Y ) } e. Top ) $=
      ( va vb vz vc vd ctop wcel cv cin wceq wrex wss cuni wi wa eqeq2d cab wal
      wral wex crn rnopab2 eqcomi sseq2i cima wfun funopabeq ssimaexg mp3an2 ex
      copab cres df-ima resopab rneqi eqtri 3eqtri eqeq2i anbi2i 3imtr3g syl5bi
      exbii imp unieq ciun cvv dfiun2g eqcomd inex1g mprg iuneq2i iunin2 uniiun
      incom a1i ineq2i 3eqtr2i syl6eq anim2i eximi syl csn snssi snex sseq1 vex
      unisn ineq1d anbi12d cla4ev sylan rexlimiva ineq1 rcla4ev expimpd exlimdv
      wb uniopn impbid2 adantr mpbird uniex eqeq1 rexbidv sylibr alrimiv clelab
      elab ceqsexv cbvrexv 3bitri inopn 3expa ineq12 inindir ancoms exp43 com24
      syl2an rexlimdva rexlimdv syl7bi com23 inex1 ralrimiva abrexexg mpbir2and
      syl6eqr istopg ) CJKZALZBLZDMZNZBCOZAUAZJKZELZYTPZUUBQZYTKZRZEUBZUUBFLZMZ
      YTKZFYTUCZEYTUCZYNUUFEYNUUCUUEYNUUCSZUUDYQNZBCOZUUEUUMUUOGLZCPZUUDUUPQZDM
      ZNZSZGUDZUUMUUQUUBYRBUUPOAUAZNZSZGUDZUVBYNUUCUVFUUCUUBYPCKZYRSBAUOZUEZPZY
      NUVFYTUVIUUBUVIYTBACYQUFUGUHYNUUBYRBAUOZCUIZPZUUQUUBUVKUUPUIZNZSZGUDZUVJU
      VFYNUVMUVQYNUVKUJUVMUVQBAYQUKGCUUBJUVKULUMUNUVLUVIUUBUVLUVKCUPZUEUVIUVKCU
      QUVRUVHYRBACURUSUTUHUVPUVEGUVOUVDUUQUVNUVCUUBUVNUVKUUPUPZUEYPUUPKZYRSBAUO
      ZUEUVCUVKUUPUQUVSUWAYRBAUUPURUSBAUUPYQUFVAVBVCVFVDVEVGUVEUVAGUVDUUTUUQUVD
      UUDUVCQZUUSUUBUVCVHUWBBUUPYQVIZBUUPDYPMZVIZUUSYQVJKZUWBUWCNBUUPUWFBUUPUCU
      WCUWBBAUUPYQVJVKVLYPDUUPVMVNBUUPYQUWDYQUWDNUVTYPDVRVSVOUWEDBUUPYPVIZMDUUR
      MUUSBUUPDYPVPUURUWGDBUUPVQVTDUURVRWAVAWBWCWDWEYNUUOUVBXAUUCYNUUOUVBUUNUVB
      BCUVGYPWFZCPZUUNUVBYPCWGUVAUWIUUNSGUWHYPWHUUPUWHNZUUQUWIUUTUUNUUPUWHCWIUW
      JUUSYQUUDUWJUURYPDUWJUURUWHQYPUUPUWHVHYPBWJWKWBWLTWMWNWOWPYNUVAUUOGYNUUQU
      UTUUOYNUUQSUURCKZUUTUUORUUPCXBUWKUUTUUOUUNUUTBUURCYPUURNYQUUSUUDYPUURDWQT
      WRUNWEWSWTXCXDXEYSUUOAUUDUUBEWJZXFYOUUDNYRUUNBCYOUUDYQXGXHXLXIUNXJYNUUKEY
      TYNUUBYTKZSZUUJFYTUWNUUHYTKZSUUIYQNZBCOZUUJUWNUWOUWQUWOUUHYQNZBCOZUWNUWQY
      SUWSAUUHFWJYOUUHNYRUWRBCYOUUHYQXGXHXLYNUWMUWSUWQRYNUWSUWMUWQUWMUUBHLZDMZN
      ZHCOZYNUWSUWQUWMYOUUBNZYSSAUDUUBYQNZBCOZUXCYSAUUBXKYSUXFAUUBUWLUXDYRUXEBC
      YOUUBYQXGXHXMUXEUXBBHCYPUWTNYQUXAUUBYPUWTDWQTXNXOUWSUUHILZDMZNZICOYNUXCUW
      QRZUWRUXIBICYPUXGNYQUXHUUHYPUXGDWQTXNYNUXIUXJICYNUXCUXIUXGCKZUWQYNUXBUXIU
      XKUWQRRHCYNUWTCKZSZUXKUXIUXBUWQUXMUXKUXIUXBUWQUXMUXKSUWTUXGMZCKZUUIUXNDMZ
      NZUWQUXIUXBSYNUXLUXKUXOUWTUXGCXPXQUXBUXIUXQUXBUXISUUIUXAUXHMUXPUUBUXAUUHU
      XHXRUWTUXGDXSYLXTUWPUXQBUXNCYPUXNNYQUXPUUIYPUXNDWQTWRYCYAYBYDYBYEVEYFYGVG
      VEVGYSUWQAUUIUUBUUHUWLYHYOUUINYRUWPBCYOUUIYQXGXHXLXIYIYIYNYTVJKUUAUUGUULS
      XABACYQJYJEFVJYTYMWEYK $.
      $( [8-Apr-2007] $)
  $}

  ${
    $d x y $.
    $( The singleton of the empty set is a topology.  (Contributed by Stefan
       Allan, 3-Mar-2006.) $)
    sn0top $p |- { (/) } e. Top $=
      ( vx vy c0 csn ctop wcel cv wss cuni wi wal cin wral wceq unieq 0ex elsnc
      sylibr sylbi cvv wo sssn uni0 elsnc2 mpbir syl6eqel unisn mpan2 vex uniex
      eqtr syl jaoi ax-gen elsn ineq2 in0 syl6eq inex1 adantl rgen2a wa wb p0ex
      istopg ax-mp mpbir2an ) CDZEFZAGZVHHZVJIZVHFZJZAKZVJBGZLZVHFZBVHMAVHMZVNA
      VKVJCNZVJVHNZUAVMVJCUBVTVMWAVTVLCIZVHVJCOWBVHFWBCNUCWBCPUDUEUFWAVLVHIZNZV
      MVJVHOWDVLCNZVMWDWCCNWECPUGVLWCCUKUHVLCVJAUIZUJQRULUMSUNVRABVHVPVHFZVRVJV
      HFWGVPCNZVRBCUOWHVQCNVRWHVQVJCLCVPCVJUPVJUQURVQCVJVPWFUSQRSUTVAVHTFVIVOVS
      VBVCVDABTVHVEVFVG $.
      $( [3-Mar-2006] $)
  $}

  ${
    $d x y A $.
    $( The indiscrete topology on a set ` A ` .  Part of Example 2 in [Munkres]
       p. 77.  (Contributed by FL, 16-Jul-2006; hypothesis eliminated by Stefan
       Allan, 6-Nov-2008.) $)
    indistop $p |- { (/) , A } e. Top $=
      ( vx vy cvv wcel c0 cuni wi cin wceq wo unieq 0ex syl6eqel jaod wa adantr
      a1i ex syl6eq cpr ctop cv wss wal wral csn sspr uni0 prid1 eqeltri unisng
      unisn sylan9eqr prid2g eqeltrd cun uniprg mpan uncom eqtri syl5bi alrimiv
      un0 vex elpr simpr ineq2d simpl ineq1d incom ineq12 adantl ccased expdimp
      in0 inidm ralrimiv wb prex istopg syl mpbir2and wn prprc2 sn0top pm2.61i
      ) ADEZFAUAZUBEZWHWJBUCZWIUDZWKGZWIEZHZBUEZWKCUCZIZWIEZCWIUFZBWIUFZWHWOBWL
      WKFJZWKFUGZJZKZWKAUGZJZWKWIJZKZKWHWNWKFAUHWHXEWNXIWHXBWNXDXBWNHWHXBWMFGZW
      IWKFLXJFWIUIFAMUJZUKNRXDWNHWHXDWMXCGZWIWKXCLXLFWIFMUMXKUKNROWHXGWNXHWHXGW
      NWHXGPWMAWIXGWHWMXFGAWKXFLADULUNWHAWIEZXGFADUOZQUPSWHXHWNWHXHPWMAWIXHWHWM
      WIGZAWKWILWHXOFAUQZAFDEWHXOXPJMFADDURUSXPAFUQAFAUTAVDVATUNWHXMXHXNQUPSOOV
      BVCWHWTBWIWKWIEXBWKAJZKZWHWTWKFABVEVFWHXRWTWHXRPZWSCWIWQWIEWQFJZWQAJZKZXS
      WSWQFACVEVFWHXRYBWSWHXBXTXQYAWSXBXTPZWSHWHYCWRFWIYCWRWKFIZFYCWQFWKXBXTVGV
      HWKVPZTXKNRXQXTPZWSHWHYFWRFWIYFWRYDFYFWQFWKXQXTVGVHYETXKNRXBYAPZWSHWHYGWR
      FWIYGWRFWQIZFYGWKFWQXBYAVIVJYHWQFIFFWQVKWQVPVATXKNRWHXQYAPZWSWHYIPZWRAWIY
      JWRAAIZAYIWRYKJWHWKAWQAVLVMAVQTWHXMYIXNQUPSVNVOVBVRSVBVRWHWIDEZWJWPXAPVSY
      LWHFAVTRBCDWIWAWBWCWHWDWIXCUBFAWEWFNWG $.
      $( [6-Nov-2008] $)
  $}

  ${
    $d x y z A $.
    indistop.1 $e |- A e. _V $.
    $( The discrete topology on a set ` A ` .  Part of Example 2 in [Munkres]
       p. 77.  (Contributed by FL, 18-Jul-2006.) $)
    distop $p |- ~P A e. Top $=
      ( vx vy cpw ctop wcel cv wss cuni wal cin wral uniss unipw syl6sseq elpw2
      wi sylbi cvv sylibr ax-gen ssinss1 syl6ibr com12 ralrimiv rgen wa wb pwex
      a1i istopg ax-mp mpbir2an ) AEZFGZCHZUOIZUQJZUOGZRZCKZUQDHZLZUOGZDUOMZCUO
      MZVACURUSAIUTURUSUOJAUQUONAOPUSABQUAUBVFCUOUQUOGZVEDUOVHUQAIZVCUOGZVERUQA
      BQVJVIVEVJVCAIZVIVERVCABQVKVIVDAIZVEVIVLRVKUQVCAUCUKVDABQUDSUESUFUGUOTGUP
      VBVGUHUIABUJCDTUOULUMUN $.
      $( [17-Jul-2006] $)

    $( The finite complement topology on a set ` A ` .  Example 3 in [Munkres]
       p. 77.  (Contributed by FL, 15-Aug-2006.) $)
    fctop $p |- { x | ( x C_ A /\ ( ( A \ x ) e. Fin \/ x = (/) ) ) }
           e. Top $=
      ( vy vz wss cdif cfn wcel c0 wceq wo wa cin cvv sseq1 difeq2 eleq1d eqeq1
      cuni cv cab ctop wal wral uniss crab simpl a1i ss2rabi ax-mp rabab unieqi
      wi unimax 3sstr3i syl6ss wn wrex uni0c notbii rexnal bitr4i ssel2 orbi12d
      vex anbi12d elab sylib simprd ord con1d imp elssuni sscon syl ssfi sylan2
      expcom ad2antlr exp31 rexlimdv syl5bi orrd uniex sylanbrc ax-gen ad2antrr
      mpd ssinss1 cun difindi unfi syl5eqel orcd ineq1 incom eqtri syl6eq ineq2
      in0 olcd ccase2 ad2ant2l jca anbi12i inex1 3imtr4i rgen2a wb abssexg mp2b
      istopg mpbir2an ) AUAZBFZBXOGZHIZXOJKZLZMZAUBZUCIZDUAZYBFZYDTZYBIZUNZDUDZ
      YDEUAZNZYBIZEYBUEDYBUEZYHDYEYFBFZBYFGZHIZYFJKZLZYGYEYFYBTZBYDYBUFYAAOUGZT
      ZXPAOUGZTZYSBYTUUBFUUAUUCFYAXPAOYAXPUNXOOIXPXTUHUIUJYTUUBUFUKYTYBYAAULUMB
      OIZUUCBKCABOUOUKUPUQYEYPYQYEYQYPYQURZYJJKZURZEYDUSZYEYPUUEUUFEYDUEZURUUHY
      QUUIEYDUTVAUUFEYDVBVCYEUUGYPEYDYEYJYDIZUUGYPYEUUJMZUUGMBYJGZHIZYPUUKUUGUU
      MUUKUUMUUFUUKUUMUUFUUKYJBFZUUMUUFLZUUKYJYBIZUUNUUOMZYDYBYJVDYAUUQAYJEVFXO
      YJKZXPUUNXTUUOXOYJBPUURXRUUMXSUUFUURXQUULHXOYJBQRXOYJJSVEVGVHZVIVJVKVLVMU
      UJUUMYPUNYEUUGUUMUUJYPUUJUUMYOUULFZYPUUJYJYFFUUTYJYDVNYJYFBVOVPUULYOVQVRV
      SVTWIWAWBWCVLWDYAYNYRMAYFYDDVFZWEXOYFKZXPYNXTYRXOYFBPUVBXRYPXSYQUVBXQYOHX
      OYFBQRXOYFJSVEVGVHWFWGYLDEYBYDBFZBYDGZHIZYDJKZLZMZUUQMZYKBFZBYKGZHIZYKJKZ
      LZMZYDYBIZUUPMYLUVIUVJUVNUVCUVJUVGUUQYDYJBWJWHUVGUUOUVNUVCUUNUVEUUMUVFUUF
      UVNUVEUUMMZUVLUVMUVQUVKUVDUULWKHBYDYJWLUVDUULWMWNWOUVFUVMUVLUVFYKJYJNZJYD
      JYJWPUVRYJJNJJYJWQYJXAWRWSXBUUFUVMUVLUUFYKYDJNJYJJYDWTYDXAWSXBXCXDXEUVPUV
      HUUPUUQYAUVHAYDUVAXOYDKZXPUVCXTUVGXOYDBPUVSXRUVEXSUVFUVSXQUVDHXOYDBQRXOYD
      JSVEVGVHUUSXFYAUVOAYKYDYJUVAXGXOYKKZXPUVJXTUVNXOYKBPUVTXRUVLXSUVMUVTXQUVK
      HXOYKBQRXOYKJSVEVGVHXHXIUUDYBOIYCYIYMMXJCXTABOXKDEOYBXMXLXN $.
      $( [26-Jun-2011] $) $( [15-Aug-2006] $)

    $( The finite complement topology on a set ` A ` .  Example 3 in [Munkres]
       p. 77.  (This version of ~ fctop requires the Axiom of Infinity.)
       (Contributed by FL, 20-Aug-2006.) $)
    fctop2 $p |- { x | ( x C_ A /\ ( ( A \ x ) ~< om \/ x = (/) ) ) } e. Top $=
      ( cv wss cdif cfn wcel c0 wceq wo wa cab com csdm isfinite3 orbi1i anbi2i
      wbr ctop abbii fctop eqeltrri ) ADZBEZBUDFZGHZUDIJZKZLZAMUEUFNOSZUHKZLZAM
      TUJUMAUIULUEUGUKUHUFPQRUAABCUBUC $.
      $( [26-Jun-2011] $) $( [19-Aug-2006] $)

    $( The countable complement topology on a set ` A ` .  Example 4 in
       [Munkres] p. 77.  (Contributed by FL, 29-Aug-2006.) $)
    cctop $p |- { x | ( x C_ A /\ ( ( A \ x ) ~<_ om \/ x = (/) ) ) } e. Top $=
      ( vy vz wss cdif com cdom wbr c0 wceq wo wcel cuni cin cvv sseq1 difeq2
      wa cv cab ctop wi wal wral uniss crab simpl a1i ax-mp rabab unieqi unimax
      ss2rabi 3sstr3i syl6ss wn wrex uni0c notbii rexnal bitr4i ssel2 vex eqeq1
      breq1d orbi12d anbi12d elab sylib ord con1d imp simpllr elssuni syl sscon
      simprd difexg ssdom2g mp2b 3syl domtr sylancom mpdan rexlimdv syl5bi orrd
      exp31 uniex sylanbrc ax-gen ssinss1 ad2antrr difindi unctb syl5eqbr ineq1
      cun orcd incom in0 eqtri syl6eq ineq2 ccase2 ad2ant2l jca anbi12i 3imtr4i
      olcd inex1 rgen2a wb abssexg istopg mpbir2an ) AUAZBFZBXSGZHIJZXSKLZMZTZA
      UBZUCNZDUAZYFFZYHOZYFNZUDZDUEZYHEUAZPZYFNZEYFUFDYFUFZYLDYIYJBFZBYJGZHIJZY
      JKLZMZYKYIYJYFOZBYHYFUGYEAQUHZOZXTAQUHZOZUUCBUUDUUFFUUEUUGFYEXTAQYEXTUDXS
      QNXTYDUIUJUOUUDUUFUGUKUUDYFYEAULUMBQNZUUGBLCABQUNUKUPUQYIYTUUAYIUUAYTUUAU
      RZYNKLZURZEYHUSZYIYTUUIUUJEYHUFZURUULUUAUUMEYHUTVAUUJEYHVBVCYIUUKYTEYHYIY
      NYHNZUUKYTYIUUNTZUUKTZBYNGZHIJZYTUUOUUKUURUUOUURUUJUUOUURUUJUUOYNBFZUURUU
      JMZUUOYNYFNZUUSUUTTZYHYFYNVDYEUVBAYNEVEXSYNLZXTUUSYDUUTXSYNBRUVCYBUURYCUU
      JUVCYAUUQHIXSYNBSVGXSYNKVFVHVIVJZVKVSVLVMVNUUPUURYSUUQIJZYTUUPUURTZYNYJFZ
      YSUUQFZUVEUVFUUNUVGYIUUNUUKUURVOYNYHVPVQYNYJBVRUUHUUQQNUVHUVEUDCBYNQVTYSU
      UQQWAWBWCYSUUQHWDWEWFWJWGWHVMWIYEYRUUBTAYJYHDVEZWKXSYJLZXTYRYDUUBXSYJBRUV
      JYBYTYCUUAUVJYAYSHIXSYJBSVGXSYJKVFVHVIVJWLWMYPDEYFYHBFZBYHGZHIJZYHKLZMZTZ
      UVBTZYOBFZBYOGZHIJZYOKLZMZTZYHYFNZUVATYPUVQUVRUWBUVKUVRUVOUVBYHYNBWNWOUVO
      UUTUWBUVKUUSUVMUURUVNUUJUWBUVMUURTZUVTUWAUWEUVSUVLUUQWTHIBYHYNWPUVLUUQWQW
      RXAUVNUWAUVTUVNYOKYNPZKYHKYNWSUWFYNKPKKYNXBYNXCXDXEXLUUJUWAUVTUUJYOYHKPKY
      NKYHXFYHXCXEXLXGXHXIUWDUVPUVAUVBYEUVPAYHUVIXSYHLZXTUVKYDUVOXSYHBRUWGYBUVM
      YCUVNUWGYAUVLHIXSYHBSVGXSYHKVFVHVIVJUVDXJYEUWCAYOYHYNUVIXMXSYOLZXTUVRYDUW
      BXSYOBRUWHYBUVTYCUWAUWHYAUVSHIXSYOBSVGXSYOKVFVHVIVJXKXNUUHYFQNYGYMYQTXOCY
      DABQXPDEQYFXQWBXR $.
      $( [23-Aug-2006] $)

    $( The indiscrete topology on a set ` A ` expressed as a topological
       space.  (Contributed by FL, 19-Jul-2006.) $)
    indistpsOLD $p |- <. A , { (/) , A } >. e. TopSpOLD $=
      ( c0 cpr cuni cop ctpsOLD cun 0ex unipr uncom un0 3eqtri opeq1i wcel ctop
      indistop eltopspOLD mpbir eqeltrri ) CADZEZUAFZAUAFGUBAUAUBCAHACHACAIBJCA
      KALMNUCGOUAPOAQUARST $.
      $( [17-Jul-2006] $)

    $( The discrete topology on a set ` A ` expressed as a topological space.
       (Contributed by FL, 20-Aug-2006.) $)
    distpsOLD $p |- <. A , ~P A >. e. TopSpOLD $=
      ( cpw cuni cop ctpsOLD unipw opeq1i wcel distop eltopspOLD mpbir eqeltrri
      ctop ) ACZDZOEZAOEFPAOAGHQFIONIABJOKLM $.
      $( [19-Aug-2006] $)
  $}

  ${
    indistpsx.a $e |- A e. _V $.
    indistpsx.k $e |- K = { <. 1 , A >. , <. 9 , { (/) , A } >. } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using explicit structure component references.  Compare with ~ indistps
       and ~ indistps2 .  The advantage of this version is that the actual
       function for the structure is evident, and ~ df-ndx is not needed, nor
       any other special definition outside of basic set theory.  The
       disadvantage is that if the indices of the component definitions
       ~ df-base and ~ df-topset are changed in the future, this theorem will
       also have to be changed.  Note:  This theorem has hard-coded structure
       indices for demonstration purposes.  It is not intended for general use;
       use ~ indistps instead.  (Contributed by FL, 19-Jul-2006.) $)
    indistpsx $p |- K e. TopSp $=
      ( vs c0 cpr c1 cop c9 cnx cbs cfv cts basendx opeq1i df-topset 9nn ndxarg
      cun preq12i eqtr4i cuni 0ex unipr uncom un0 3eqtrri indistop eltpsi ) AFA
      GZBBHAIZJUKIZGKLMZAIZKNMZUKIZGDUOULUQUMUNHAOPUPJUKNJEEQRSPUAUBUKUCFATAFTA
      FAUDCUEFAUFAUGUHAUIUJ $.
      $( [19-Oct-2012] $)
  $}

  ${
    indistps.a $e |- A e. _V $.
    indistps.k $e |- K
      = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , { (/) , A } >. } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using implicit structure indices.  The advantage of this version over
       ~ indistpsx is that it is independent of the indices of the component
       definitions ~ df-base and ~ df-topset , and if they are changed in the
       future, this theorem will not be affected.  The advantage over
       ~ indistps2 is that it is easy to eliminate the hypotheses with ~ eqid
       and ~ vtoclg to result in a closed theorem.  Theorems ~ indistpsALT and
       ~ indistps2ALT show that the two forms can be derived from each other.
       (Contributed by FL, 19-Jul-2006.) $)
    indistps $p |- K e. TopSp $=
      ( c0 cpr cuni cun 0ex unipr uncom un0 3eqtrri indistop eltpsi ) AEAFZBDPG
      EAHAEHAEAICJEAKALMANO $.
      $( [24-Oct-2012] $)
  $}

  ${
    indistps2.a $e |- ( Base ` K ) = A $.
    indistps2.j $e |- ( TopOpen ` K ) = { (/) , A } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using direct component assignments.  Compare with ~ indistps .  The
       advantage of this version is that it is the shortest to state and
       easiest to work with in most situations.  Theorems ~ indistpsALT and
       ~ indistps2ALT show that the two forms can be derived from each
       other. $)
    indistps2 $p |- K e. TopSp $=
      ( cpr cuni cun 0ex cbs cfv cvv fvex eqeltrri unipr uncom 3eqtrri indistop
      c0 un0 istpsi ) ARAEZBCDUAFRAGARGARAHBIJAKCBILMNRAOASPAQT $.
      $( [24-Oct-2012] $)
  $}

  ${
    indistpsALT.a $e |- A e. _V $.
    indistpsALT.k $e |- K
      = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , { (/) , A } >. } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological
       space.  Here we show how to derive the structural version ~ indistps
       from the direct component assignment version ~ indistps2 . $)
    indistpsALT $p |- K e. TopSp $=
      ( cbs cfv cvv wcel wceq cpr tpsbase ax-mp eqcomi prex tpstopset indistps2
      c0 cts ) ABABEFZAGHASICAQAJZBGDKLMTBRFZTGHTUAIQANATBGDOLMP $.
      $( [24-Oct-2012] $)
  $}

  ${
    indistps2ALT.a $e |- ( Base ` K ) = A $.
    indistps2ALT.j $e |- ( TopOpen ` K ) = { (/) , A } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using direct component assignments.  Here we show how to derive the
       direct component assignment version ~ indistps2 from the structural
       version ~ indistps . $)
    indistps2ALT $p |- K e. TopSp $=
      ( ctps wcel cnx cbs cfv cop cts c0 cpr fvex eqeltrri eqid indistps tpsstr
      cvv mpbir ) BEFGHIAJGKILAMZJMZEFAUBBHIASCBHNOUBPZQAUABUBCDUCRT $.
      $( [24-Oct-2012] $)
  $}

  ${
    distps.a $e |- A e. _V $.
    distps.k $e |- K
             = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , ~P A >. } $.
    $( The discrete topology on a set ` A ` expressed as a topological space.
       (Contributed by FL, 20-Aug-2006.) $)
    distps $p |- K e. TopSp $=
      ( cpw cuni unipw eqcomi distop eltpsi ) AAEZBDKFAAGHACIJ $.
      $( [19-Oct-2012] $)
  $}

