$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Homeo $. $( Class of all homeomorphisms. $)
  $c ~= $. $( The relation "is homeomorph to." $)

  $( Extend class notation with the class of all homeomorphisms. $)
  chomeosm $a class Homeo $.

  $( Extend class notation with the relation "is homeomorph to.". $)
  chomeo $a class ~= $.

  ${
    $d j k f x $.
    $( Function returning all the homeomorphisms from topology ` j ` to
       topology ` k ` . $)
    df-homeo $a |- Homeo = ( j e. Top , k e. Top |->
            { f | ( f : U. j -1-1-onto-> U. k /\ A. x e. j ( f " x ) e. k /\
                    A. x e. k ( `' f " x ) e. j ) } ) $.
  $}

  ${
    $d x y f $.
    $( Definition of the relation ` x ` is homeomorph to ` y ` . $)
    df-hmph $a |- ~= = ( ( Top X. Top ) \ ( `' Homeo " { (/) } ) ) $.
  $}

  ${
    $d J f j k x $.  $d K f j k x $.  $d X f j k $.  $d Y f j k $.
    homeofval.1 $e |- X = U. J $.
    homeofval.2 $e |- Y = U. K $.
    $( The set of all the homeomorphisms between two topologies.  (Contributed
       by FL, 20-Feb-2007.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    homeofval $p |- ( ( J e. Top /\ K e. Top ) -> ( J Homeo K ) =
    { f | ( f : X -1-1-onto-> Y /\ A. x e. J ( f " x ) e. K /\
    A. x e. K ( `' f " x ) e. J ) } ) $=
      ( vj vk ctop wcel cv wf1o wral w3a cab cvv wceq cuni cima ccnv co wa cmap
      chomeosm wss wf 3ad2ant1 wb uniexg syl5eqel elmapg syl2anr syl5ibr abssdv
      f1of ssexg sylancl unieq syl6eqr f1oeq2 syl raleq eleq2 ralbidv 3anbi123d
      ovex abbidv f1oeq3 df-homeo ovmpt2g mpd3an3 ) CKLZDKLZEFBMZNZVPAMZUAZDLZA
      COZVPUBVRUAZCLZADOZPZBQZRLZCDUFUCWFSVNVOUDZWFFEUEUCZUGWIRLWGWHWEBWIWEVPWI
      LZWHEFVPUHZVQWAWKWDEFVPUQUIVOFRLERLWJWKUJVNVOFDTZRHDKUKULVNECTZRGCKUKULFE
      VPRRUMUNUOUPFEUEVHWFWIRURUSIJCDKKIMZTZJMZTZVPNZVSWPLZAWNOZWBWNLZAWPOZPZBQ
      WFUFEWQVPNZWSACOZWCAWPOZPZBQRWNCSZXCXGBXHWRXDWTXEXBXFXHWOESWRXDUJXHWOWMEW
      NCUTGVAWOEWQVPVBVCWSAWNCVDXHXAWCAWPWNCWBVEVFVGVIWPDSZXGWEBXIXDVQXEWAXFWDX
      IWQFSXDVQUJXIWQWLFWPDUTHVAWQFEVPVJVCXIWSVTACWPDVSVEVFWCAWPDVDVGVIABIJVKVL
      VM $.
      $( [15-Dec-2013] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F f x $.  $d J f x $.  $d K f x $.  $d X f $.  $d Y f $.
    ishomeo.1 $e |- X = U. J $.
    ishomeo.2 $e |- Y = U. K $.
    $( The predicate F is a homeomorphism between topology ` J ` and topology
       ` K ` .  Based on Bourbaki TG I.2.  (Contributed by FL, 20-Feb-2007.)
       (Revised by Mario Carneiro, 30-May-2014.) $)
    ishomeo $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Homeo K ) <->
      ( F : X -1-1-onto-> Y /\
        A. x e. J ( F " x ) e. K /\
        A. x e. K ( `' F " x ) e. J ) ) ) $=
      ( vf ctop wcel cv wf1o cima wral ccnv w3a cvv wi a1i cab homeofval eleq2d
      wa chomeosm co elex wf cuni uniexg adantr syl5eqel 3ad2ant1 expcom syl2im
      f1of fex wb wceq f1oeq1 imaeq1 eleq1d ralbidv imaeq1d 3anbi123d pm5.21ndd
      cnveq elabg bitrd ) CJKZDJKZUDZBCDUEUFZKBEFILZMZVNALZNZDKZACOZVNPZVPNZCKZ
      ADOZQZIUAZKZEFBMZBVPNZDKZACOZBPZVPNZCKZADOZQZVLVMWEBAICDEFGHUBUCVLBRKZWFW
      OWFWPSVLBWEUGTVLERKZWOEFBUHZWPVLECUIZRGVJWSRKVKCJUJUKULWGWJWRWNEFBUPUMWRW
      QWPEFRBUQUNUOWPWFWOURSVLWDWOIBRVNBUSZVOWGVSWJWCWNEFVNBUTWTVRWIACWTVQWHDVN
      BVPVAVBVCWTWBWMADWTWAWLCWTVTWKVPVNBVGVDVBVCVEVHTVFVI $.
      $( [30-May-2014] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    hmeomap.1 $e |- X = U. J $.
    hmeomap.2 $e |- Y = U. K $.
    $( A homeomorphism is a 1-1-onto mapping.  (Contributed by FL,
       5-Mar-2007.) $)
    hmeomap $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> F : X -1-1-onto-> Y ) ) $=
      ( vx ctop wcel wa chomeosm co wf1o cv cima wral ccnv w3a ishomeo syl6bi
      simp1 ) BIJCIJKABCLMJDEANZAHOZPCJHBQZARUDPBJHCQZSUCHABCDEFGTUCUEUFUBUA $.
      $( [30-May-2014] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The image of an open set by the converse of a homeomorphism is an open
       set.  (Contributed by FL, 5-Mar-2007.) $)
    hmeocna $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. K ( `' F " x ) e. J ) ) $=
      ( ctop wcel wa chomeosm co cuni wf1o cv cima wral ccnv eqid ishomeo simp3
      w3a syl6bi ) CEFDEFGBCDHIFCJZDJZBKZBALZMDFACNZBOUDMCFADNZSUFABCDUAUBUAPUB
      PQUCUEUFRT $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The image of an open set by a homeomorphism is an open set.
       (Contributed by FL, 5-Mar-2007.) $)
    hmeocnb $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. J ( F " x ) e. K ) ) $=
      ( ctop wcel wa chomeosm co cuni wf1o cv cima wral ccnv eqid ishomeo simp2
      w3a syl6bi ) CEFDEFGBCDHIFCJZDJZBKZBALZMDFACNZBOUDMCFADNZSUEABCDUAUBUAPUB
      PQUCUEUFRT $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.  $d X x $.  $d Y x $.
    $( A homeomorphism is a bicontinuous bijection.  (Contributed by FL,
       1-Sep-2008.)  (Revised by Mario Carneiro, 30-May-2014.) $)
    hmeobc $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Homeo K ) <->
                   ( F e. ( J Cn K ) /\ `' F e. ( K Cn J ) ) ) ) $=
      ( vx ctop wcel wa cuni wf1o cima wral ccnv ccn co wf eqid iscn adantr syl
      wb w3a chomeosm f1of adantl biantrurd bitr4d ancoms f1ocnv wrel wceq frel
      dfrel2 sylib imaeq1d eleq1d ralbidv 3bitr2d anbi12d ancom syl6bb pm5.32da
      3anass syl6rbbr ishomeo wfn cnf 3expa ffn ancom1s anim12dan dff1o4 sylibr
      cv ex pm4.71rd 3bitr4d ) BEFZCEFZGZBHZCHZAIZADVMZJZCFZDBKZALZWCJBFDCKZUAZ
      WBABCMNFZWGCBMNFZGZGZABCUBNFWLVSWMWBWFWHGZGWIVSWBWLWNVSWBGZWLWHWFGWNWOWJW
      HWKWFWOWJVTWAAOZWHGZWHVSWJWQTWBDABCVTWAVTPZWAPZQRWOWPWHWBWPVSVTWAAUCUDZUE
      UFWOWKWAVTWGOZWGLZWCJZCFZDBKZGZXEWFVSWKXFTZWBVRVQXGDWGCBWAVTWSWRQUGRWOXAX
      EWOWAVTWGIZXAWBXHVSVTWAAUHUDWAVTWGUCSUEWOXDWEDBWOXCWDCWOXBAWCWOAUIZXBAUJW
      OWPXIWTVTWAAUKSAULUMUNUOUPUQURWHWFUSUTVAWBWFWHVBVCDABCVTWAWRWSVDVSWLWBVSW
      LWBVSWLGAVTVEZWGWAVEZGWBVSWJXJWKXKVSWJGWPXJVQVRWJWPABCVTWAWRWSVFVGVTWAAVH
      SVSWKGXAXKVRVQWKXAVRVQWKXAWGCBWAVTWSWRVFVGVIWAVTWGVHSVJVTWAAVKVLVNVOVP $.
      $( [30-May-2014] $) $( [23-Aug-2008] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The converse of a homeomorphism is a homeomorphism.  (Contributed by FL,
       5-Mar-2007.) $)
    cnvhmpha $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> `' F e. ( K Homeo J ) ) ) $=
      ( ctop wcel wa ccn co ccnv chomeosm wrel wceq w3a cuni wf eqid cnf hmeobc
      frel syl 3expa dfrel2 sylib simpr eqeltrd ancld expimpd wb ancoms 3imtr4d
      a1d ) BDEZCDEZFZABCGHZEZAIZCBGHEZFURUQIZUOEZFZABCJHEUQCBJHEZUNUPURVAUNUPF
      ZURUTVCUTURVCUSAUOVCAKZUSALULUMUPVDULUMUPMBNZCNZAOVDABCVEVFVEPVFPQVEVFAST
      UAAUBUCUNUPUDUEUKUFUGABCRUMULVBVAUHUQCBRUIUJ $.
      $( [30-May-2014] $) $( [5-Mar-2007] $)
  $}

  ${
    hmeores.1 $e |- X = U. J $.
    $( The restriction of a homeomorphism is a homeomorphism.  (Contributed by
       Mario Carneiro, 14-Sep-2014.) $)
    hmeores $p |- ( ( J e. Top /\ K e. Top /\ Y C_ X ) ->
      ( F e. ( J Homeo K ) -> ( F |` Y ) e.
       ( ( subSp ` <. Y , J >. ) Homeo ( subSp ` <. ( F " Y ) , K >. ) ) ) ) $=
      ( ctop wcel wss chomeosm co wa ccn wb syl22anc crn cvv syl syl2anc wf1o
      w3a cres cop csubsp cfv simpl1 simpl2 simpl3 hmeobc 3adant3 biimpa simpld
      cima ccnv cnsubsp cuni topopn ssexg stoig3 df-ima eqimss2i a1i imassrn wf
      eqid cnf syl3anc syl5ss cnsubsp2 mpbid wfun wceq wi hmeomap f1ocnv f1ofun
      frn imp funcnvres 3syl simprd eqeltrd imaexg adantl wf1 f1of1 f1ores f1of
      mpbir2and ex ) BGHZCGHZEDIZUAZABCJKZHZAEUBZEBUCUDUEZAEUMZCUCUDUEZJKHZWNWP
      LZXAWQWRWTMKHZWQUNZWTWRMKHZXBWQWRCMKHZXCXBWKWLWMABCMKHZXFWKWLWMWPUFZWKWLW
      MWPUGZWKWLWMWPUHZXBXGAUNZCBMKHZWNWPXGXLLZWKWLWPXMNWMABCUIUJUKZULZEABCDFUO
      OXBWRGHZWLWQPZWSIZWSCUPZIZXFXCNXBWKEQHZXPXHXBWMDBHZYAXJXBWKYBXHBDFUQREDBU
      RSEBQUSSZXIXRXBWSXQAEUTVAVBXBWSAPZXSAEVCXBDXSAVDZYDXSIXBWKWLXGYEXHXIXOABC
      DXSFXSVEZVFVGDXSAVQRVHZWSWQWRCXSYFVIOVJXBXDWTBMKZHZXEXBXDXKWSUBZYHXBXSDXK
      TZXKVKXDYJVLXBDXSATZYKWNWPYLWKWLWPYLVMWMABCDXSFYFVNUJVRZDXSAVORXSDXKVPEAV
      SVTXBWLWKXTXLYJYHHXIXHYGXBXGXLXNWAWSXKCBXSYFUOOWBXBWTGHZWKXDPEIZWMYIXENXB
      WLWSQHZYNXIWPYPWNAEWOWCWDWSCQUSSZXHXBWSEXDTZWSEXDVDYOXBEWSWQTZYRXBDXSAWEZ
      WMYSXBYLYTYMDXSAWFRXJDXSEAWGSEWSWQVORWSEXDWHWSEXDVQVTXJEXDWTBDFVIOVJXBXPY
      NXAXCXELNYCYQWQWRWTUISWIWJ $.
      $( [14-Sep-2014] $)
  $}

  ${
    $d J f $.  $d K f $.  $d f x y z $.
    $( Express the predicate ` J ` is homeomorph to ` K ` .  (Contributed by
       FL, 20-Feb-2007.)  (Revised by Mario Carneiro, 30-May-2014.) $)
    hmph $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K <->
                 ( J Homeo K ) =/= (/) ) ) $=
      ( vx vy vf vz chomeo wbr ctop chomeosm ccnv c0 cima wa wcel co df-br wceq
      sylibr cv cxp csn wn wne cdif df-hmph breqi brdif bitri opelxpi biantrurd
      cop cfv wb wfn cvv cuni wf1o wral w3a cab df-homeo cmap ovex wf vex uniex
      f1of elmap 3ad2ant1 abssi ssexi fnmpt2i 0ex fniniseg mp2an baib syl df-ov
      eqeq1i 3bitr4g necon3bbid bitr3d syl5bb ) ABGHZABIIUAZHZABJKLUBMZHZUCZNZA
      IOBIONZABJPZLUDZWEABWFWHUEZHWKABGWOUFUGABWFWHUHUIWLWJWKWNWLWGWJWLABULZWFO
      ZWGABIIUJZABWFQSUKWLWIWMLWLWPWHOZWPJUMZLRZWIWMLRWLWQWSXAUNWRWSWQXAJWFUOLU
      POWSWQXANUNCDIICTZUQZDTZUQZETZURZXFFTZMXDOFXBUSZXFKXHMXBOFXDUSZUTZEVAZJFE
      CDVBXLXEXCVCPZXEXCVCVDXKEXMXGXIXFXMOZXJXGXCXEXFVEXNXCXEXFVHXEXCXFXDDVFVGX
      BCVFVGVISVJVKVLVMVNWFLWPJUPVOVPVQVRABWHQWMWTLABJVSVTWAWBWCWD $.
      $( [30-May-2014] $) $( [14-Feb-2007] $)
  $}

  ${
    $d f J $.  $d f K $.
    $( Compactness is a topological property-that is, for any two homeomorphic
       topologies, either both are compact or neither is.  (Contributed by Jeff
       Hankins, 30-Jun-2009.) $)
    cmptoppr $p |- ( ( J e. Top /\ K e. Top /\ J ~= K ) ->
                      ( J e. Comp <-> K e. Comp ) ) $=
      ( vf ctop wcel chomeo ccmp wa co cuni wfo ccn wf1o eqid f1ofo jca32 cncmp
      3impia expl syl5com wbr wb cv chomeosm wex c0 wne hmph syl6bb w3a hmeomap
      n0 simp2 syl ccnv hmeobc biimp3a simpld simp1 f1ocnv simprd impbid 3expia
      3syl exlimdv sylbid ) ADEZBDEZABFUAZAGEZBGEZUBZVGVHHZVICUCZABUDIZEZCUEZVL
      VMVIVOUFUGVQABUHCVOULUIVMVPVLCVGVHVPVLVGVHVPUJZVJVKVRVHAJZBJZVNKZVNABLIEZ
      HZHVJVKVRVHWAWBVGVHVPUMVRVSVTVNMZWAVGVHVPWDVNABVSVTVSNZVTNZUKRZVSVTVNOUNV
      RWBVNUOZBALIEZVGVHVPWBWIHVNABUPUQZURPVJVHWCVKVNABVSVTWEWFQSTVRVGVTVSWHKZW
      IHZHVKVJVRVGWKWIVGVHVPUSVRWDVTVSWHMWKWGVSVTVNUTVTVSWHOVDVRWBWIWJVAPVKVGWL
      VJWHBAVTVSWFWEQSTVBVCVEVFR $.
      $( [1-Dec-2010] $) $( [30-Jun-2009] $)
  $}

  ${
    $d f J $.  $d f K $.
    $( Connectedness is a topological property.  (Contributed by Jeff Hankins,
       3-Jul-2009.) $)
    conntoppr $p |- ( ( J e. Top /\ K e. Top /\ J ~= K ) -> ( J e. Con <-> K e.
    Con ) ) $=
      ( vf ctop wcel chomeo ccon wa cuni wfo ccn wf1o 3impia f1ofo jca32 cnconn
      co eqid expl syl5com wbr wb cv chomeosm wex c0 wne hmph n0 syl6bb hmeomap
      w3a simp2 syl ccnv hmeobc biimp3a simpld f1ocnv 3syl simprd impbid 3expia
      simp1 exlimdv sylbid ) ADEZBDEZABFUAZAGEZBGEZUBZVGVHHZVICUCZABUDQZEZCUEZV
      LVMVIVOUFUGVQABUHCVOUIUJVMVPVLCVGVHVPVLVGVHVPULZVJVKVRVHAIZBIZVNJZVNABKQE
      ZHZHVJVKVRVHWAWBVGVHVPUMVRVSVTVNLZWAVGVHVPWDVNABVSVTVSRZVTRZUKMZVSVTVNNUN
      VRWBVNUOZBAKQEZVGVHVPWBWIHVNABUPUQZUROVJVHWCVKVNABVSVTWEWFPSTVRVGVTVSWHJZ
      WIHZHVKVJVRVGWKWIVGVHVPVDVRWDVTVSWHLWKWGVSVTVNUSVTVSWHNUTVRWBWIWJVAOVKVGW
      LVJWHBAVTVSWFWEPSTVBVCVEVFM $.
      $( [3-Jul-2009] $)
  $}

  ${
    $d k x y A $.  $d k x y J $.  $d x y K $.  $d k x y ph $.  $d k x y X $.
    pt1hmeo.x $e |- X = U. J $.
    pt1hmeo.j $e |- K = ( Xt_ ` { <. A , J >. } ) $.
    pt1hmeo.a $e |- ( ph -> A e. V ) $.
    pt1hmeo.r $e |- ( ph -> J e. Top ) $.
    $( The canonical homeomorphism from a topological product on a singleton to
       the topology of the factor. $)
    pt1hmeo $p |- ( ph -> ( x e. X |-> { <. A , x >. } ) e. ( J Homeo K ) ) $=
      ( vk vy csn wcel wa wceq adantr cvv ctop cv cop cmpt chomeosm co ccn ccnv
      cxp fconstmpt sneq xpeq1d opeq1 sneqd eqeq12d vex xpsn vtoclg syl syl5eqr
      mpteq2dva snex a1i wf wss wf1o f1osng syl2anc snssd fss cfv cnmptid elsni
      f1of fveq2d fvsng sylan9eqr oveq2d eleqtrrd ptcn eqeltrrd cuni cmap copab
      simprr adantrr eqtr4d simprl eqid wb topopn elmapg sylancl mpbird eqeltrd
      fmptd fveq1d eqtr2d jca mpbid snidg ffvelrn feq2d fveq2 eleq1d id opeq12d
      eqeq2d anbi12d bibi12d fsn2 simprd opeq2d impbida opabbidv df-mpt cnvopab
      cnveqi eqtri 3eqtr4g cixp wral unieqd syl6reqr ralrimiva ixpeq2 ixpconstg
      sylancr ptuni 3eqtr3d mpteq1 eqtrd ptpjcn syl3anc eleqtrd syl5eqel hmeobc
      cpt pttop mpbir2and ) ABGCBUAZUBZNZUCZDEUDUEOZUUCDEUFUEZOZUUCUGZEDUFUEZOZ
      ABGLCNZYTUCZUCUUCUUEABGUUKUUBAYTGOZPZUUKUUJYTNZUHZUUBLUUJYTUIUUMCFOZUUOUU
      BQZAUUPUULJRLUAZNZUUNUHZUURYTUBZNZQUUQLCFUURCQZUUTUUOUVBUUBUVCUUSUUJUUNUU
      RCUJZUKUVCUVAUUAUURCYTULUMUNUURYTLUOZBUOUPUQURUSZUTABYTLCDUBNZUUJDESGHIKU
      UJSOZACVAZVBZAUUJDNZUVGVCZUVKTVDUUJTUVGVCZAUUJUVKUVGVEZUVLAUUPDTOZUVNJKCD
      FTVFVGUUJUVKUVGVMURADTKVHUUJUVKTUVGVIVGZAUURUUJOZPZBGYTUCZDDUFUEZDUURUVGV
      JZUFUEAUVSUVTOUVQABDGHKVKRUVRUWADDUFUVQAUWACUVGVJZDUVQUURCUVGUURCVLVNAUUP
      UVOUWBDQJKCDFTVOVGZVPZVQVRVSVTAUUGMEWAZCMUAZVJZUCZUUHAUUGMGUUJWBUEZUWGUCZ
      UWHAUULUWFUUBQZPZMBWCZUWFUWIOZYTUWGQZPZMBWCUUGUWJAUWLUWPMBAUWLUWPAUWLPZUW
      NUWOUWQUWFUUKUWIUWQUWFUUBUUKAUULUWKWDZAUULUUKUUBQUWKUVFWEWFUWQUUKUWIOZUUJ
      GUUKVCZUWQLUUJYTGUUKUWQUULUVQAUULUWKWGZRUUKWHWOUWQGDOZUVHUWSUWTWIAUXBUWLA
      UVOUXBKDGHWJURZRUVIGUUJUUKDSWKWLWMWNUWQUWGCUUBVJZYTUWQCUWFUUBUWRWPUWQUUPU
      ULUXDYTQAUUPUWLJRUXACYTFGVOVGWQWRAUWPPZUULUWKUXEYTUWGGAUWNUWOWDZUXEUUJGUW
      FVCZCUUJOZUWGGOZUXEUWNUXGAUWNUWOWGUXEUXBUVHUWNUXGWIAUXBUWPUXCRUVIGUUJUWFD
      SWKWLWSZAUXHUWPAUUPUXHJCFWTURZRUUJGCUWFXAVGWNUXEUWFCUWGUBZNZUUBUXEUXIUWFU
      XMQZUXEUXGUXIUXNPZUXJUXEUUPUXGUXOWIZAUUPUWPJRUUSGUWFVCZUURUWFVJZGOZUWFUUR
      UXRUBZNZQZPZWIUXPLCFUVCUXQUXGUYCUXOUVCUUSUUJGUWFUVDXBUVCUXSUXIUYBUXNUVCUX
      RUWGGUURCUWFXCZXDUVCUYAUXMUWFUVCUXTUXLUVCUURCUXRUWGUVCXEUYDXFUMXGXHXIUURG
      UWFUVEXJUQURWSXKUXEUUAUXLUXEYTUWGCUXFXLUMWFWRXMXNUUGUWLBMWCZUGUWMUUCUYEBM
      GUUBXOXQUWLBMXPXRMBUWIUWGXOXSAUWIUWEQUWJUWHQALUUJGXTZLUUJUWAWAZXTZUWIUWEA
      GUYGQZLUUJYAUYFUYHQAUYILUUJUVRUYGDWAGUVRUWADUWDYBHYCYDLUUJGUYGYEURAUVHUXB
      UYFUWIQUVIUXCLUUJGSDYFYGAUVHUVMUYHUWEQUVJUVPLUUJUVGESIYHVGYIMUWIUWEUWGYJU
      RYKAUWHEUWBUFUEZUUHAUVHUVMUXHUWHUYJOUVJUVPUXKMUUJUVGCESUWEUWEWHIYLYMAUWBD
      EUFUWCVQYNWNAUVOETOUUDUUFUUIPWIKAEUVGYQVJZTIAUVHUVMUYKTOUVIUVPUUJUVGSYRYG
      YOUUCDEYPVGYS $.
      $( [3-Feb-2015] $)
  $}

  ${
    $d k n w x y z A $.  $d k u z G $.  $d v w x y z J $.  $d k u w x y z ph $.
    $d k n w x y z B $.  $d k u x y z K $.  $d k u x y z L $.  $d w x y z X $.
    $d k n u v w x y z C $.  $d k n u v w x y z F $.  $d k n u v w z V $.
    $d w x y z Y $.
    ptunhmeo.x $e |- X = U. K $.
    ptunhmeo.y $e |- Y = U. L $.
    ptunhmeo.j $e |- J = ( Xt_ ` F ) $.
    ptunhmeo.k $e |- K = ( Xt_ ` ( F |` A ) ) $.
    ptunhmeo.l $e |- L = ( Xt_ ` ( F |` B ) ) $.
    ptunhmeo.g $e |- G = ( x e. X , y e. Y |-> ( x u. y ) ) $.
    ptunhmeo.c $e |- ( ph -> C e. V ) $.
    ptunhmeo.f $e |- ( ph -> F : C --> Top ) $.
    ptunhmeo.u $e |- ( ph -> C = ( A u. B ) ) $.
    ptunhmeo.i $e |- ( ph -> ( A i^i B ) = (/) ) $.
    $( Exhibit the converse function of the map ` G ` which joins two product
       topologies on disjoint index sets.  (Contributed by Mario Carneiro,
       8-Feb-2015.) $)
    ptuncnv $p |- ( ph ->
      `' G = ( z e. U. J |-> <. ( z |` A ) , ( z |` B ) >. ) ) $=
      ( vw vk cv cxp wcel c1st cfv c2nd cun wceq wa copab cuni cres ccnv simprr
      cop cmpt cixp cdif wss xp1st ad2antrl ixpeq2 fvres unieqd mprg ctop ssun1
      cvv wf syl5sseqr ssexg syl2anc fssres ptuni syl5eqr adantr eleqtrrd xp2nd
      syl6eqr ssun2 eqcomd cin c0 wb uneqdifeq mpbid ixpeq1 syl syl3anc eleqtrd
      undifixp eqeltrd 1st2nd2 reseq1d wfn ixpfn fnresdm incom syl5eq fnresdisj
      resundir uneq12d un0 syl6eq eqtr2d uncom eqtr3i 3eqtr4g opeq12d eqtrd jca
      simprl resixp opelxpi fveq2d vex resex op1st reseq2d resundi syl6req 3syl
      op2nd 3eqtrrd impbida opabbidv cmpt2 fveq2 mpt2mpt df-mpt 3eqtr2i cnvopab
      cnveqi eqtri ) AUFUHZNOUIZUJZDUHZUUBUKULZUUBUMULZUNZUOZUPZDUFUQZUUEJURZUJ
      ZUUBUUEEUSZUUEFUSZVBZUOZUPZDUFUQIUTZDUULUUPVCAUUJUURDUFAUUJUURAUUJUPZUUMU
      UQUUTUUEUUHUULAUUDUUIVAZUUTUUHUGGUGUHZHULZURZVDZUULUUTUUFUGEUVDVDZUJZUUGU
      GGEVEZUVDVDZUJEGVFZUUHUVEUJUUTUUFNUVFUUDUUFNUJAUUIUUBNOVGVHAUVFNUOZUUJAUV
      FKURZNAUVFUGEUVBHEUSZULZURZVDZUVLUVOUVDUOUVPUVFUOUGEUGEUVOUVDVIUVBEUJUVNU
      VCUVBEHVJVKVLAEVOUJZEVMUVMVPZUVPUVLUOAUVJGMUJZUVQAEFUNZEGEFVNUDVQZUBEGMVR
      VSAGVMHVPZUVJUVRUCUWAGVMEHVTVSUGEUVMKVOSWAVSWBPWFZWCWDZUUTUUGUGFUVDVDZUVI
      UUTUUGOUWEUUDUUGOUJAUUIUUBNOWEVHAUWEOUOZUUJAUWELURZOAUWEUGFUVBHFUSZULZURZ
      VDZUWGUWJUVDUOUWKUWEUOUGFUGFUWJUVDVIUVBFUJUWIUVCUVBFHVJVKVLAFVOUJZFVMUWHV
      PZUWKUWGUOAFGVFZUVSUWLAUVTFGFEWGUDVQZUBFGMVRVSAUWBUWNUWMUCUWOGVMFHVTVSUGF
      UWHLVOTWAVSWBQWFZWCWDZAUVIUWEUOZUUJAUVHFUOZUWRAUVTGUOZUWSAGUVTUDWHAUVJEFW
      IZWJUOZUWTUWSWKUWAUEEFGWLVSWMUGUVHFUVDWNWOWCWDAUVJUUJUWAWCUGGEUVDUUFUUGWR
      WPAUVEUULUOZUUJAUVSUWBUXCUBUCUGGHJMRWAVSZWCWQWSUUTUUBUUFUUGVBZUUPUUDUUBUX
      EUOAUUIUUBNOWTVHUUTUUFUUNUUGUUOUUTUUNUUHEUSZUUFUUTUUEUUHEUVAXAUUTUXFUUFWJ
      UNZUUFUUTUXFUUFEUSZUUGEUSZUNUXGUUFUUGEXHUUTUXHUUFUXIWJUUTUUFEXBZUXHUUFUOU
      UTUVGUXJUWDUGUUFEUVDXCWOZEUUFXDWOUUTFEWIZWJUOZUXIWJUOZUUTUXLUXAWJFEXEAUXB
      UUJUEWCZXFUUTUUGFXBZUXMUXNWKUUTUUGUWEUJUXPUWQUGUUGFUVDXCWOZFEUUGXGWOWMXIX
      FUUFXJXKXLUUTUUOUUHFUSZUUGUUTUUEUUHFUVAXAUUTUUFFUSZUUGFUSZUNWJUUGUNZUXRUU
      GUUTUXSWJUXTUUGUUTUXBUXSWJUOZUXOUUTUXJUXBUYBWKUXKEFUUFXGWOWMUUTUXPUXTUUGU
      OUXQFUUGXDWOXIUUFUUGFXHUUGWJUNUUGUYAUUGXJUUGWJXMXNXOXLXPXQXRAUURUPZUUDUUI
      UYCUUBUUPUUCAUUMUUQVAZUYCUUNNUJUUOOUJUUPUUCUJUYCUUNUVFNUYCUVJUUEUVEUJZUUN
      UVFUJAUVJUURUWAWCUYCUUEUULUVEAUUMUUQXSAUXCUURUXDWCWDZUGGEUVDUUEXTVSAUVKUU
      RUWCWCWQUYCUUOUWEOUYCUWNUYEUUOUWEUJAUWNUURUWOWCUYFUGGFUVDUUEXTVSAUWFUURUW
      PWCWQUUNUUONOYAVSWSUYCUUHUUNUUOUNZUUEGUSZUUEUYCUUFUUNUUGUUOUYCUUFUUPUKULU
      UNUYCUUBUUPUKUYDYBUUNUUOUUEEDYCZYDZYEXKUYCUUGUUPUMULUUOUYCUUBUUPUMUYDYBUU
      NUUOUYJUUEFUYIYDYJXKXIUYCUYHUUEUVTUSUYGUYCGUVTUUEAGUVTUOUURUDWCYFUUEEFYGY
      HUYCUYEUUEGXBUYHUUEUOUYFUGUUEGUVDXCGUUEXDYIYKXRYLYMUUSUUJUFDUQZUTUUKIUYKI
      BCNOBUHZCUHZUNZYNUFUUCUUHVCUYKUABCUFNOUUHUYNUUBUYLUYMVBZUOZUUFUYLUUGUYMUY
      PUUFUYOUKULUYLUUBUYOUKYOUYLUYMBYCZYEXKUYPUUGUYOUMULUYMUUBUYOUMYOUYLUYMUYQ
      CYCYJXKXIYPUFDUUCUUHYQYRYTUUJUFDYSUUADUFUULUUPYQXO $.
      $( [8-Feb-2015] $)

    $( Define a homeomorphism from a binary product of infinite product
       topologies to an infinite product topologies on the union of the index
       sets.  This is the topological analogue of
       ` ( A ^ B ) x. ( A ^ C ) = A ^ ( B + C ) ` .  (Contributed by Mario
       Carneiro, 8-Feb-2015.) $)
    ptunhmeo $p |- ( ph -> G e. ( ( K tX L ) Homeo J ) ) $=
      ( vz vk vu vw vn vv ctx co chomeosm wcel ccn ccnv cuni wf cv cima csn cfv
      cmpt cmpt2 crn cun wral cxp cixp cdif wss simprl wceq ixpeq2 fvres unieqd
      wa cres mprg cvv ctop ssun1 syl5sseqr ssexg syl2anc ptuni syl5eqr syl6eqr
      fssres adantr eleqtrrd simprr eqcomd cin c0 wb uneqdifeq mpbid ixpeq1 syl
      ssun2 eqtrd undifixp syl3anc eleqtrd fmpt2 sylib cpt pttop syl5eqel txuni
      ralrimivva feq2d fimacnv txtop eqid topopn eqeltrd eleq1d syl5ibrcom wrex
      ccom fveq1 w3a wfn 3ad2ant1 3adant1r ad2antrr simp1r syl112anc mpt2eq3dva
      ixpfn simpr ptpjcn adantl oveq2d cnmpt21 cab mpbir2and ptrescn imaeq2d wi
      elsni ralrimiv wal imaco cnvco adantlr a1i fmpt2co cnveqd imaeq1d ffvelrn
      eqidd elun simp2 simp3 fvun1 cnmpt1st fvun2 cnmpt2nd mpdan cnima syl31anc
      jaodan imaeq2 rexlimdvva alrimiv rnmpt2 raleqi eqeq1 2rexbidv ralab bitri
      wo sylibr ralunb sylanbrc cfi ctg snex fvex abrexex rgenw abrexex2g unexg
      sylancl sylancr ptval2 subbascn cop ptuncnv cnmpt1t hmeobc ) AHJKUKULZIUM
      ULUNZHUWOIUOULUNZHUPZIUWOUOULZUNZAUWQUWOUQZIUQZHURZUWRUEUSZUTZUWOUNZUEUXB
      VAZUFUGFUFUSZGVBZUHUXBUXHUHUSZVBZVCZUPZUGUSZUTZVDZVEZVFZVGZAMNVHZUXBHURZU
      XCABUSZCUSZVFZUXBUNZCNVGBMVGUYAAUYEBCMNAUYBMUNZUYCNUNZVQZVQZUYDUIFUIUSZGV
      BZUQZVIZUXBUYIUYBUIDUYLVIZUNZUYCUIFDVJZUYLVIZUNDFVKZUYDUYMUNUYIUYBMUYNAUY
      FUYGVLAUYNMVMZUYHAUYNJUQZMAUYNUIDUYJGDVRZVBZUQZVIZUYTVUCUYLVMVUDUYNVMUIDU
      IDVUCUYLVNUYJDUNVUBUYKUYJDGVOVPVSADVTUNZDWAVUAURZVUDUYTVMAUYRFLUNZVUEADEV
      FZDFDEWBUCWCZUADFLWDWEZAFWAGURZUYRVUFUBVUIFWADGWIWEZUIDVUAJVTRWFWEWGOWHZW
      JWKUYIUYCNUYQAUYFUYGWLAUYQNVMUYHAUYQUIEUYLVIZNAUYPEVMZUYQVUNVMAVUHFVMZVUO
      AFVUHUCWMAUYRDEWNWOVMZVUPVUOWPVUIUDDEFWQWEWRUIUYPEUYLWSWTAVUNKUQZNAVUNUIE
      UYJGEVRZVBZUQZVIZVURVVAUYLVMVVBVUNVMUIEUIEVVAUYLVNUYJEUNVUTUYKUYJEGVOVPVS
      AEVTUNZEWAVUSURZVVBVURVMAEFVKZVUGVVCAVUHEFEDXAUCWCZUAEFLWDWEZAVUKVVEVVDUB
      VVFFWAEGWIWEZUIEVUSKVTSWFWEWGPWHZXBWJWKAUYRUYHVUIWJUIFDUYLUYBUYCXCXDAUYMU
      XBVMZUYHAVUGVUKVVJUAUBUIFGILQWFWEWJXEZXLBCMNUYDUXBHTXFXGAUXTUXAUXBHAJWAUN
      ZKWAUNZUXTUXAVMAJVUAXHVBZWARAVUEVUFVVNWAUNVUJVULDVUAVTXIWEXJZAKVUSXHVBZWA
      SAVVCVVDVVPWAUNVVGVVHEVUSVTXIWEXJZJKMNOPXKWEXMWRZAUXFUEUXGVGUXFUEUXQVGZUX
      SAUXFUEUXGAUXFUXDUXGUNZUWRUXBUTZUWOUNAVWAUXAUWOAUXCVWAUXAVMVVRUXAUXBHXNWT
      AUWOWAUNZUXAUWOUNAVVLVVMVWBVVOVVQJKXOWEZUWOUXAUXAXPZXQWTXRVVTUXEVWAUWOVVT
      UXDUXBUWRUXDUXBUUCUUAXSXTUUDAUXDUXOVMZUGUXIYAUFFYAZUXFUUBZUEUUEZVVSAVWGUE
      AVWEUXFUFUGFUXIAUXHFUNZUXNUXIUNZVQZVQZUXFVWEUWRUXOUTZUWOUNVWLVWMBCMNUXHUY
      DVBZVDZUPZUXNUTZUWOVWLVWMUWRUXMYBZUXNUTVWQUWRUXMUXNUUFVWLVWRVWPUXNVWLVWRU
      XLHYBZUPVWPUXLHUUGVWLVWSVWOVWLBCUHMNUXBUYDUXKVWNHUXLAUYHUYEVWKVVKUUHHBCMN
      UYDVDVMVWLTUUIVWLUXLUUNUXHUXJUYDYCUUJUUKWGUULWGVWLVWBUXIWAUNZVWOUWOUXIUOU
      LZUNZVWJVWQUWOUNAVWBVWKVWCWJVWLVUKVWIVWTAVUKVWKUBWJAVWIVWJVLZFWAUXHGUUMWE
      ZVWLUXHDUNZUXHEUNZUVOZVXBVWLUXHVUHUNVXGVWLUXHFVUHVXCAFVUHVMVWKUCWJXEUXHDE
      UUOXGVWLVXEVXBVXFVWLVXEVQZVWOBCMNUXHUYBVBZVDVXAVXHBCMNVWNVXIVXHUYFUYGYDUY
      BDYEZUYCEYEZVUQVXEVWNVXIVMVWLUYFUYGVXJVXEVWLUYFUYGYDZUYOVXJVXLUYBMUYNVWLU
      YFUYGUUPVWLUYFUYSUYGAUYSVWKVUMWJYFWKUIUYBDUYLYLWTZYGVWLUYFUYGVXKVXEVXLUYC
      VUNUNVXKVXLUYCNVUNVWLUYFUYGUUQVWLUYFVUNNVMZUYGAVXNVWKVVIWJYFWKUIUYCEUYLYL
      WTZYGVXHUYFVUQUYGAVUQVWKVXEUDYHYFVWLVXEUYFUYGYIDEUYBUYCUXHUURYJYKVXHBCUEU
      YBUXHUXDVBZVXIJKJUXIMNMOPAVVLVWKVXEVVOYHZAVVMVWKVXEVVQYHZVXQVWLVWTVXEVXDW
      JVXHBCJKMNOPVXQVXRUUSOVXHUEMVXPVCZJUXHVUAVBZUOULZJUXIUOULVXHVUEVUFVXEVXSV
      YAUNAVUEVWKVXEVUJYHAVUFVWKVXEVULYHVWLVXEYMUEDVUAUXHJVTMORYNXDVXHVXTUXIJUO
      VXEVXTUXIVMVWLUXHDGVOYOYPXEUXHUXDUYBYCYQXRVWLVXFVQZVWOBCMNUXHUYCVBZVDVXAV
      YBBCMNVWNVYCVYBUYFUYGYDVXJVXKVUQVXFVWNVYCVMVWLUYFUYGVXJVXFVXMYGVWLUYFUYGV
      XKVXFVXOYGVYBUYFVUQUYGAVUQVWKVXFUDYHYFVWLVXFUYFUYGYIDEUYBUYCUXHUUTYJYKVYB
      BCUEUYCVXPVYCJKKUXIMNNOPAVVLVWKVXFVVOYHZAVVMVWKVXFVVQYHZVYEVWLVWTVXFVXDWJ
      VYBBCJKMNOPVYDVYEUVAPVYBUENVXPVCZKUXHVUSVBZUOULZKUXIUOULVYBVVCVVDVXFVYFVY
      HUNAVVCVWKVXFVVGYHAVVDVWKVXFVVHYHVWLVXFYMUEEVUSUXHKVTNPSYNXDVYBVYGUXIKUOV
      XFVYGUXIVMVWLUXHEGVOYOYPXEUXHUXDUYCYCYQXRUVEUVBAVWIVWJWLUXNVWOUWOUXIUVCUV
      DXRVWEUXEVWMUWOUXDUXOUWRUVFXSXTUVGUVHVVSUXFUEUJUSZUXOVMZUGUXIYAZUFFYAZUJY
      RZVGVWHUXFUEUXQVYMUFUGUJFUXIUXOUXPUXPXPZUVIZUVJVYLVWFUXFUEUJVYIUXDVMVYJVW
      EUFUGFUXIVYIUXDUXOUVKUVLUVMUVNUVPUXFUEUXGUXQUVQUVRAVWBUXRVTUNZIUXRUVSVBUV
      TVBVMZUWQUXCUXSVQWPVWCAUXGVTUNUXQVTUNVYPUXBUWAAUXQVYMVTVYOAVUGVYKUJYRVTUN
      ZUFFVGVYMVTUNUAVYRUFFUGUJUXIUXOUXHGUWBUWCUWDVYKUFUJFLVTUWEUWGXJUXGUXQVTVT
      UWFUWHAVUGVUKVYQUAUBUHUGFUFGUXPILUXBQUXBXPZVYNUWIWEUEUXRHUWOIVTUXAUXBVWDV
      YSUWJXDYSAUWRUEUXBUXDDVRZUXDEVRZUWKVCUWSABCUEDEFGHIJKLMNOPQRSTUAUBUCUDUWL
      AUEVYTWUAIJKUXBMVYSAIGXHVBZWAQAVUGVUKWUBWAUNUAUBFGLXIWEXJZOVVOVVQAVUGVUKU
      YRUEUXBVYTVCIJUOULUNUAUBVUIUEFDGIJLUXBVYSQRYTXDAVUGVUKVVEUEUXBWUAVCIKUOUL
      UNUAUBVVFUEFEGIKLUXBVYSQSYTXDUWMXRAVWBIWAUNUWPUWQUWTVQWPVWCWUCHUWOIUWNWEY
      S $.
      $( [8-Feb-2015] $)
  $}

  ${
    $d u v x y z A $.  $d u v x y z F $.
    $( A topological product of finitely many compact spaces is compact.  This
       weak version of Tychonoff's theorem does not require the axiom of
       choice.  (Contributed by Mario Carneiro, 8-Feb-2015.) $)
    ptcmpfi $p |- ( ( A e. Fin /\ F : A --> Comp ) -> ( Xt_ ` F ) e. Comp ) $=
      ( vx cfn wcel ccmp wf cres cpt cfv wceq fveq2d wss wi cv c0 ctop cvv eqid
      syl2anc vy vz vu vv wa wfn ffn fnresdm syl adantl simpl ssid sseq1 reseq2
      csn cun res0 syl6eq eleq1d imbi2d imbi12d cin 0ex f0 pttop mp2an cuni cpw
      cixp ptuni ixp0x snfi eqeltri eqeltrri pwfi mpbi pwuni ssfi elin mpbir2an
      fincmp ax-mp wn ssun1 id syl5ss imim1i ctx co cop ad2antlr ssun2 ad2antll
      a1ii vex snss sylibr fnressn simplr ffvelrn chomeo wb cmptop ssriv sseldi
      wbr snex fss sylancl sylancr eqeltrrd chomeosm wne cmpt pt1hmeo ne0i hmph
      fssres mpbird cmptoppr syl3anc mpbid eqeltrd txcmp expcom txtop cmpt2 idi
      unex resabs1 eqcomi fveq2i a1i eqidd simprl disjsn ptunhmeo sylibd a2d ex
      syl5 findcard2s mpi mpcom ) ADEZAFBGZUEZBAHZIJZBIJFUUGUUHBIUUFUUHBKZUUEUU
      FBAUFZUUJAFBUGZABUHUIUJLUUEUUGUUIFEZUUEUUFUKUUEAAMZUUGUUMNZAULCOZAMZUUGBU
      UPHZIJZFEZNZNPAMZUUGPIJZFEZNZNUAOZAMZUUGBUVFHZIJZFEZNZNZUVFUBOZUOZUPZAMZU
      UGBUVOHZIJZFEZNZNZUUNUUONCUAUBAUUPPKZUUQUVBUVAUVEUUPPAUMUWBUUTUVDUUGUWBUU
      SUVCFUWBUURPIUWBUURBPHPUUPPBUNBUQURLUSUTVAUUPUVFKZUUQUVGUVAUVKUUPUVFAUMUW
      CUUTUVJUUGUWCUUSUVIFUWCUURUVHIUUPUVFBUNLUSUTVAUUPUVOKZUUQUVPUVAUVTUUPUVOA
      UMUWDUUTUVSUUGUWDUUSUVRFUWDUURUVQIUUPUVOBUNLUSUTVAUUPAKZUUQUUNUVAUUOUUPAA
      UMUWEUUTUUMUUGUWEUUSUUIFUWEUURUUHIUUPABUNLUSUTVAUVBUUGUVDUVCQDVBEZUVDUWFU
      VCQEZUVCDEZPREZPQPGZUWGVCQVDZPPRVEVFUVCVGZVHZDEZUVCUWMMUWHUWLDEUWNCPUUPPJ
      VGZVIZUWLDUWIUWJUWPUWLKVCUWKCPPUVCRUVCSVJVFUWPPUODCUWOVKPVLVMVNUWLVOVPUVC
      VQUWMUVCVRVFUVCQDVSVTUVCWAWBWNUVMUVFEWCZUVLUWANUVFDEUVLUVPUVKNUWQUWAUVPUV
      GUVKUVPUVFUVOAUVFUVNWDZUVPWEZWFZWGUWQUVPUVKUVTUWQUVPUVKUVTNUWQUVPUEZUUGUV
      JUVSUUGUXAUVJUVSNUUGUXAUEZUVJUVIBUVNHZIJZWHWIZFEZUVSUXBUXDFEZUVJUXFNUXBUX
      DUVMUVMBJZWJUOZIJZFUXBUXCUXIIUXBUUKUVMAEZUXCUXIKUUFUUKUUEUXAUULWKUXBUVNAM
      ZUXKUXBUVNUVOAUVNUVFWLZUVPUVPUUGUWQUWSWMZWFZUVMAUBWOWPWQZAUVMBWRTLZUXBUXH
      FEZUXJFEZUXBUUFUXKUXRUUEUUFUXAWSZUXPAFUVMBWTTZUXBUXHQEZUXJQEZUXHUXJXAXFZU
      XRUXSXBUXBFQUXHCFQUUPXCXDZUYAXEZUXBUXDUXJQUXQUXBUVNREUVNQUXCGZUXDQEZUVMXG
      ZUXBAQBGZUXLUYGUXBUUFFQMUYJUXTUYEAFQBXHXIZUXOAQUVNBXRTUVNUXCRVEXJZXKZUXBU
      YDUXHUXJXLWIZPXMZUXBCUXHVGZUVMUUPWJUOXNZUYNEUYOUXBCUVMUXHUXJAUYPUYPSUXJSU
      XPUYFXOUYNUYQXPUIUXBUYBUYCUYDUYOXBUYFUYMUXHUXJXQTXSUXHUXJXTYAYBYCUVJUXGUX
      FUVIUXDYDYEUIUXBUXEQEZUVRQEZUXEUVRXAXFZUXFUVSXBUXBUVIQEZUYHUYRUXBUVFREUVF
      QUVHGZVUAUAWOZUXBUYJUVGVUBUYKUVPUVGUUGUWQUWTWMAQUVFBXRTUVFUVHRVEXJUYLUVIU
      XDYFTZUXBUVOREZUVOQUVQGZUYSUVFUVNVUCUYIYIZUXBUYJUVPVUFUYKUXNAQUVOBXRTZUVO
      UVQRVEXJZUXBUYTUXEUVRXLWIZPXMZUXBUCUDUVIVGZUXDVGZUCOUDOUPYGZVUJEVUKUXBUCU
      DUVFUVNUVOUVQVUNUVRUVIUXDRVULVUMVULSVUMSUVRSUVHUVQUVFHZIVUOUVHUVFUVOMZVUO
      UVHKVUPUWRYHBUVFUVOYJWBYKYLUXCUVQUVNHZIVUQUXCUVNUVOMVUQUXCKUXMBUVNUVOYJWB
      YKYLVUNSVUEUXBVUGYMVUHUXBUVOYNUXBUVFUVNVBPKZNUXBUWQVURUUGUWQUVPYOUVFUVMYP
      WQYHYQVUJVUNXPUIUXBUYRUYSUYTVUKXBVUDVUIUXEUVRXQTXSUXEUVRXTYAYRYEYSYTYSUUA
      UJUUBUUCUUDXK $.
      $( [8-Feb-2015] $)
  $}

