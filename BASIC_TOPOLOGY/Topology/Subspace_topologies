$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Subspace topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c subSp $. $( Function returning a subspace topology. $)

  $( Extend class notation with the function returning a subspace topology. $)
  csubsp $a class subSp $.

  ${
    $d x y z w $.
    $( Function returning the subspace topology induced by the topology ` y `
       and the set ` x ` . $)
    df-subsp $a |- subSp = ( x e. _V , y e. Top |->
                             { z | E. w e. y z = ( w i^i x ) } ) $.
  $}

  ${
    $d A w x y z $.  $d J w x y $.
    $( The subspace topology induced by the topology ` J ` on the set ` A ` .
       (Contributed by FL, 20-Sep-2010.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    subspval $p |- ( ( J e. Top /\ A e. V )
       -> ( subSp ` <. A , J >. ) = { x | E. y e. J x = ( y i^i A ) } ) $=
      ( vz vw ctop wcel wa cop csubsp cv cin wceq wrex cab cvv wss abbidv df-ov
      cfv co elex adantl simpl cpw inss2 vex inex1 mpbir eleq1 mpbiri rexlimivw
      abssi pwexg ssexg sylancr syl ineq2 eqeq2d rexbidv rexeq df-subsp ovmpt2g
      elpw syl3anc syl5eqr ) DHIZCEIZJZCDKLUBCDLUCZAMZBMZCNZOZBDPZAQZCDLUAVKCRI
      ZVIVRRIZVLVROVJVSVICEUDUEZVIVJUFVKVSVTWAVSVRCUGZSWBRIVTVQAWBVPVMWBIZBDVPW
      CVOWBIZWDVOCSVNCUHVOCVNCBUIUJVFUKVMVOWBULUMUNUOCRUPVRWBRUQURUSFGCDRHVMVNF
      MZNZOZBGMZPZAQVRLVPBWHPZAQRWECOZWIWJAWKWGVPBWHWKWFVOVMWECVNUTVAVBTWHDOWJV
      QAVPBWHDVCTFGABVDVEVGVH $.
      $( [15-Dec-2013] $) $( [20-Sep-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d J x y $.
    $( The predicate "is an open set of a subspace topology".  (Contributed by
       FL, 28-Jan-2009.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    elsubsp $p |- ( ( J e. Top /\ B e. V ) ->
      ( A e. ( subSp ` <. B , J >. ) <-> E. x e. J A = ( x i^i B ) ) ) $=
      ( vy ctop wcel wa cop csubsp cfv cv cin wceq wrex cab subspval eleq2d cvv
      vex inex1 eleq1 mpbiri rexlimivw eqeq1 rexbidv elab3 syl6bb ) DGHCEHIZBCD
      JKLZHBFMZAMZCNZOZADPZFQZHBUNOZADPZUJUKUQBFACDERSUPUSFBURBTHZADURUTUNTHUMC
      AUAUBBUNTUCUDUEULBOUOURADULBUNUFUGUHUI $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d x A $.  $d x J $.  $d x S $.
    $( Sufficient condition for being an open set in a subspace.  (Contributed
       by Jeff Hankins, 11-Jul-2009.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    elsubspr $p |- ( ( J e. Top /\ S e. V /\ A e. J ) ->
                  ( A i^i S ) e. ( subSp ` <. S , J >. ) ) $=
      ( vx ctop wcel cin cop csubsp cfv wa cv wceq wrex eqid ineq1 eqeq2d mpan2
      rcla4ev elsubsp syl5ibr 3impia ) CFGZBDGZACGZABHZBCIJKGZUFUHUDUELUGEMZBHZ
      NZECOZUFUGUGNZULUGPUKUMEACUIANUJUGUGUIABQRTSEUGBCDUAUBUC $.
      $( [15-Dec-2013] $) $( [11-Jul-2009] $)
  $}

  ${
    $d o t J $.  $d o t X $.
    subspid.1 $e |- X = U. J $.
    $( The subspace topology of the base set is the original topology.
       (Contributed by Jeff Hankins, 9-Jul-2009.) $)
    subspid $p |- ( J e. Top -> ( subSp ` <. X , J >. ) = J ) $=
      ( vo vt ctop wcel cop csubsp cfv cv cin wceq wrex wb cuni uniexg syl5eqel
      cvv wa elsubsp mpdan topopn adantr inopn eleq1 syl5ibrcom rexlimdva simpr
      mpd3an3 eltopss df-ss sylib eqcomd ineq1 eqeq2d rcla4ev syl2anc ex impbid
      wss bitrd eqrdv ) AFGZDBAHIJZAVDDKZVEGZVFEKZBLZMZEANZVFAGZVDBSGVGVKOVDBAP
      SCAFQREVFBASUAUBVDVKVLVDVJVLEAVDVHAGZTVLVJVIAGZVDVMBAGZVNVDVOVMABCUCUDVHB
      AUEUJVFVIAUFUGUHVDVLVKVDVLTZVLVFVFBLZMZVKVDVLUIVPVQVFVPVFBVAVQVFMVFABCUKV
      FBULUMUNVJVREVFAVHVFMVIVQVFVHVFBUOUPUQURUSUTVBVC $.
      $( [15-Dec-2013] $) $( [9-Jul-2009] $)
  $}

  ${
    $d x y A $.  $d x y J $.
    $( A subspace topology is a topology.  (Contributed by FL, 5-Jan-2009.)
       (Revised by Mario Carneiro, 15-Dec-2013.) $)
    stoig3 $p |- ( ( J e. Top /\ A e. V ) -> ( subSp ` <. A , J >. ) e. Top )
        $=
      ( vx vy ctop wcel cop csubsp cfv cin wceq wrex cab subspval subtop adantr
      wa cv eqeltrd ) BFGZACGZRABHIJDSESAKLEBMDNZFDEABCOUAUCFGUBDEBAPQT $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)

    $( The underlying set of a subspace topology.  (Contributed by FL,
       5-Jan-2009.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    stoig2 $p |- ( ( J e. Top /\ A C_ U. J ) ->
                   A = U. ( subSp ` <. A , J >. ) ) $=
      ( vx vy ctop wcel cuni wss wa cop csubsp cfv cin wceq simpr sseqin2 sylib
      cvv syl cv simpl id uniexg ssexg syl2anr topopn elsubspr syl3anc eqeltrrd
      eqid elssuni wrex cab subspval syldan unieqd inss2 inex1 elpw mpbir eleq1
      cpw vex mpbiri rexlimivw abssi sspwuni mpbi a1i eqsstrd eqssd ) BEFZABGZH
      ZIZAABJKLZGZVOAVPFAVQHVOVMAMZAVPVOVNVRANVLVNOAVMPQVOVLARFZVMBFZVRVPFVLVNU
      AZVNVNVMRFVSVLVNUBBEUCAVMRUDUEZVOVLVTWABVMVMUJUFSVMABRUGUHUIAVPUKSVOVQCTZ
      DTZAMZNZDBULZCUMZGZAVOVPWHVLVNVSVPWHNWBCDABRUNUOUPWIAHZVOWHAVBZHWJWGCWKWF
      WCWKFZDBWFWLWEWKFZWMWEAHWDAUQWEAWDADVCURUSUTWCWEWKVAVDVEVFWHAVGVHVIVJVK
      $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  $( The topological space built with a subspace topology.  (Contributed by FL,
     5-Jan-2009.)  (The proof was shortened by Mario Carneiro, 15-Dec-2013.) $)
  stoig $p |- ( ( J e. Top /\ A C_ U. J ) ->
   { <. ( Base ` ndx ) , A >. ,
       <. ( TopOpen ` ndx ) , ( subSp ` <. A , J >. ) >. } e. TopSp ) $=
    ( ctop wcel cuni wss wa cnx cbs cfv cop cts csubsp cpr ctps wceq cvv uniexg
    wb id ssexg syl2anr fvex eqid eltpsg sylancl stoig3 syldan stoig2 mpbir2and
    ) BCDZABEZFZGZHIJAKHLJABKZMJZKNZODZUPCDZAUPEPZUNAQDZUPQDURUSUTGSUMUMULQDVAU
    KUMTBCRAULQUAUBZUOMUCAQQUPUQUQUDUEUFUKUMVAUSVBABQUGUHABUIUJ $.
    $( [15-Dec-2013] $) $( [21-Oct-2012] $)

  ${
    $d w x y z B $.  $d w x y A $.  $d w x y z J $.
    $( Composition of subspaces.  (Contributed by Mario Carneiro,
       15-Dec-2013.) $)
    subspco $p |- ( ( J e. Top /\ A e. V /\ B e. W ) ->
                     ( subSp ` <. A , ( subSp ` <. B , J >. ) >. ) =
                       ( subSp ` <. ( A i^i B ) , J >. ) ) $=
      ( vx vy vz vw ctop wcel cv cin wceq wrex cab cop csubsp cfv subspval in32
      w3a inex1 ineq1 inass eqtri syl6eq abrexco 3adant2 opeq2d fveq2d eqeltrrd
      vex stoig3 simp2 syl2anc eqtrd cvv simp1 inex1g syl 3eqtr4a ) CJKZADKZBEK
      ZUBZFLZGLZAMZNGHLILZBMZNICOHPZOFPZVGVJABMZMZNICOFPZABCQRSZQZRSZVNCQRSZFGH
      ICVKVIVOVJBIUMUCVHVKNVIVKAMZVOVHVKAUDWAVJAMBMVOVJBAUAVJABUEUFUGUHVFVSAVLQ
      ZRSZVMVFVRWBRVFVQVLAVCVEVQVLNVDHIBCETUIZUJUKVFVLJKVDWCVMNVFVQVLJWDVCVEVQJ
      KVDBCEUNUIULVCVDVEUOZFGAVLDTUPUQVFVCVNURKZVTVPNVCVDVEUSVFVDWFWEABDUTVAFIV
      NCURTUPVB $.
      $( [15-Dec-2013] $)
  $}
  $( Equivalence of being a subspace of a subspace and being a subspace of the
     original.  (Contributed by Jeff Hankins, 11-Jul-2009.)  (The proof was
     shortened by Mario Carneiro, 8-Aug-2014.) $)
  subsubtop $p |- ( ( J e. Top /\ S C_ T /\ T e. V ) ->
                    ( subSp ` <. S , ( subSp ` <. T , J >. ) >. ) =
                      ( subSp ` <. S , J >. ) ) $=
    ( ctop wcel wss w3a cop csubsp cfv cin cvv wceq ssexg 3adant1 subspco simp2
    syld3an2 df-ss sylib opeq1d fveq2d eqtrd ) CEFZABGZBDFZHZABCIJKIJKZABLZCIZJ
    KZACIZJKUEAMFZUFUGUIULNUFUGUNUEABDOPABCMDQSUHUKUMJUHUJACUHUFUJANUEUFUGRABTU
    AUBUCUD $.
    $( [8-Aug-2014] $) $( [11-Jul-2009] $)

  ${
    $d x y J $.  $d x y X $.  $d x y A $.  $d y V $.
    subspint.1 $e |- X = U. J $.
    $( When the subspace region is not a subset of the base of the topology,
       the resulting set is the same as the subspace restricted to the base.
       (Contributed by Mario Carneiro, 15-Dec-2013.) $)
    subspint $p |- ( ( J e. Top /\ A e. V ) ->
      ( subSp ` <. A , J >. ) = ( subSp ` <. ( A i^i X ) , J >. ) ) $=
      ( ctop wcel wa cop csubsp cfv subspid adantr opeq2d fveq2d topopn subspco
      cin wceq mpd3an3 eqtr3d ) BFGZACGZHZADBIJKZIZJKZABIZJKADRBIJKZUDUFUHJUDUE
      BAUBUEBSUCBDELMNOUBUCDBGZUGUISUBUJUCBDEPMADBCBQTUA $.
      $( [15-Dec-2013] $)
  $}

  ${
    $d x y J $.
    $( The subspace topology induced by the topology ` J ` on the empty set.
       (Contributed by FL, 22-Dec-2008.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    subsp0 $p |- ( J e. Top -> ( subSp ` <. (/) , J >. ) = { (/) } ) $=
      ( vx vy ctop wcel c0 cop csubsp cfv csn cv wceq cin wrex cvv wb 0ex mpan2
      elsubsp in0 eqeq2i biimpi rexlimivw cuni eqid topopn eqcomi ineq1 rcla4ev
      eqeq2d sylancl eqeq1 rexbidv syl5ibrcom impbid2 bitrd elsn syl6bbr eqrdv
      ) ADEZBFAGHIZFJZUTBKZVAEZVCFLZVCVBEUTVDVCCKZFMZLZCANZVEUTFOEVDVIPQCVCFAOS
      RUTVIVEVHVECAVHVEVGFVCVFTUAUBUCUTVIVEFVGLZCANZUTAUDZAEFVLFMZLZVKAVLVLUEUF
      VMFVLTUGVJVNCVLAVFVLLVGVMFVFVLFUHUJUIUKVEVHVJCAVCFVGULUMUNUOUPBFUQURUS $.
      $( [15-Dec-2013] $) $( [22-Dec-2008] $)
  $}

  ${
    $d A x y $.  $d V x $.
    $( The only subspace topology induced by the topology ` { (/) } ` .
       (Contributed by FL, 5-Jan-2009.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    subspsn $p |- ( A e. V -> ( subSp ` <. A , { (/) } >. ) = { (/) } ) $=
      ( vx vy wcel c0 csn cop csubsp cfv wceq cin wrex ctop sn0top elsubsp mpan
      cv wb 0ex ineq1 eqeq2d rexsn incom eqtri eqeq2i bitri syl6bb elsn syl6bbr
      in0 eqrdv ) ABEZCAFGZHIJZUNUMCRZUOEZUPFKZUPUNEUMUQUPDRZALZKZDUNMZURUNNEUM
      UQVBSODUPAUNBPQVBUPFALZKZURVAVDDFTUSFKUTVCUPUSFAUAUBUCVCFUPVCAFLFFAUDAUKU
      EUFUGUHCFUIUJUL $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d A x y $.  $d J x y $.  $d X x $.
    subspsn2.1 $e |- X = U. J $.
    $( The subspace topology induced by a singleton.  (Contributed by FL,
       5-Jan-2009.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    subspsn2 $p |- ( ( J e. Top /\ A e. X ) ->
      ( subSp ` <. { A } , J >. ) = { (/) , { A } } ) $=
      ( ctop wcel wa csn cop csubsp cfv cuni wceq c0 cpr cvv snex stoig3 adantr
      mpan2 wss snssi eleq2s stoig2 sylan2 eqcomd eqid topsn syl2anc ) BEFZACFZ
      GZAHZBIJKZEFZUNLZUMMUNNUMOMUJUOUKUJUMPFUOAQUMBPRTSULUMUPUKUJUMBLZUAZUMUPM
      URAUQCAUQUBDUCUMBUDUEUFAUNUPUPUGUHUI $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d o x A $.  $d o x J $.  $d o x S $.  $d o x X $.
    subcld.1 $e |- X = U. J $.
    $( A closed set of a subspace topology is a closed set of the original
       topology intersected with the subset.  (Contributed by FL,
       11-Jul-2009.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    subcld $p |- ( ( J e. Top /\ S C_ X ) ->
                   ( A e. ( Clsd ` ( subSp ` <. S , J >. ) ) <->
                     E. x e. ( Clsd ` J ) A = ( x i^i S ) ) ) $=
      ( vo ctop wcel wss wa cfv cdif cin wceq wb cvv biimpi cun c0 ccld cuni cv
      cop csubsp wrex sseq2i uniexg ssexg syl2anr stoig3 syldan eqid syl stoig2
      iscld sseq2d difeq1d eleq1d anbi12d sylan2b elsubsp anbi2d opncld adantlr
      adantr incom df-ss syl5eq ad2antlr difeq2 difindi difid uneq2i un0 3eqtri
      ad2antrr syl6eq adantl dfss4 3eqtr2rd difeq1i indif2 3eqtr2i ineq1 eqeq2d
      rcla4ev syl2anc ex rexlimdva expimpd sylbid difin2 simpll cldopn elsubspr
      syl3anc eqeltrd inss2 jctil sseq1 syl5ibrcom impbid 3bitr2d ) DHIZCEJZKZB
      CDUDUELZUALIZBXHUBZJZXJBMZXHIZKZBCJZCBMZXHIZKZBAUCZCNZOZADUALZUFZXGXHHIZX
      IXNPXEXFCQIZYDXFCDUBZJZYFQIYEXEXFYGEYFCFUGZRDHUHCYFQUIUJZCDQUKULBXHXJXJUM
      UPUNXFXEYGXRXNPYHXEYGKZXOXKXQXMYJCXJBCDUOZUQYJXPXLXHYJCXJBYKURUSUTVAXGXRY
      CXGXRXOXPGUCZCNZOZGDUFZKYCXGXQYOXOXEXFYEXQYOPYIGXPCDQVBULVCXGXOYOYCXGXOKZ
      YNYCGDYPYLDIZKZYNYCYRYNKZEYLMZYBIZBYTCNZOZYCYRUUAYNXGYQUUAXOXEYQUUAXFYLDE
      FVDVEVEVFYSBECNZYLMZUUBYSUUECYLMZCXPMZBYSUUDCYLYPUUDCOZYQYNXFUUHXEXOXFUUD
      CENZCECVGZXFUUICOCEVHRVIVJVQURYNUUGUUFOYRYNUUGCYMMZUUFXPYMCVKUUKUUFCCMZSU
      UFTSUUFCYLCVLUULTUUFCVMZVNUUFVOVPVRVSYPUUGBOZYQYNXOUUNXGXOUUNBCVTRVSVQWAU
      UEUUIYLMCYTNUUBUUDUUIYLUUJWBCEYLWCCYTVGWDVRYAUUCAYTYBXSYTOXTUUBBXSYTCWEWF
      WGWHWIWJWKWLXGYAXRAYBXGXSYBIZKZXRYAXTCJZCXTMZXHIZKUUPUUSUUQUUPUUREXSMZCNZ
      XHXGUURUVAOUUOXGUURCXSMZUVAUURUVBUULSUVBTSUVBCXSCVLUULTUVBUUMVNUVBVOVPXFU
      VBUVAOXECXSEWMVSVIVFUUPXEYEUUTDIZUVAXHIXEXFUUOWNXGYEUUOYIVFXEUUOUVCXFXSDE
      FWOVEUUTCDQWPWQWRXSCWSWTYAXOUUQXQUUSBXTCXAYAXPUURXHBXTCVKUSUTXBWJXCXD $.
      $( [15-Dec-2013] $) $( [11-Jul-2009] $)
  $}

  ${
    $d J v $.  $d X v $.  $d A v $.  $d B v $.
    subspcld.1 $e |- X = U. J $.
    $( A closed set is closed in the subspace topology.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    subspcld $p |- ( ( ( J e. Top /\ A C_ X ) /\ ( B e. ( Clsd ` J )
                /\ B C_ A ) ) -> B e. ( Clsd ` ( subSp ` <. A , J >. ) ) ) $=
      ( vv ctop wcel wss wa ccld cfv cop csubsp cv cin wceq wrex simprl dfss wb
      biimpi ad2antll ineq1 eqeq2d rcla4ev syl2anc subcld adantr mpbird ) CGHAD
      IJZBCKLZHZBAIZJZJZBACMNLKLHZBFOZAPZQZFULRZUPUMBBAPZQZVAUKUMUNSUNVCUKUMUNV
      CBATUBUCUTVCFBULURBQUSVBBURBAUDUEUFUGUKUQVAUAUOFBACDEUHUIUJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A v $.  $d B v $.  $d J v $.
    $( A set which is closed in the subspace topology induced by a closed set
       is closed in the original topology.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    subspcld2 $p |- ( ( J e. Top /\ A e. ( Clsd ` J ) /\
            B e. ( Clsd ` ( subSp ` <. A , J >. ) ) ) -> B e. ( Clsd ` J ) ) $=
      ( vv ctop wcel ccld cfv cop csubsp wa cv cin wceq wrex cuni wb eqid cldss
      wss subcld syldan incld 3expa an32s syl5ibrcom rexlimdva sylbid 3impia
      eleq1 ) CEFZACGHZFZBACIJHGHFZBULFZUKUMKZUNBDLZAMZNZDULOZUOUKUMACPZTUNUTQA
      CVAVARZSDBACVAVBUAUBUPUSUODULUPUQULFZKUOUSURULFZUKVCUMVDUKVCUMVDUQACUCUDU
      EBURULUJUFUGUHUI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x J $.  $d x K $.  $d x S $.  $d x X $.  $d x Y $.
    subcls.1 $e |- X = U. J $.
    subcls.2 $e |- K = ( subSp ` <. Y , J >. ) $.
    $( A closure in a subspace topology.  (Contributed by Jeff Hankins,
       22-Jan-2010.) $)
    subcls $p |- ( ( J e. Top /\ Y C_ X /\ S C_ Y ) -> ( ( cls ` K ) ` S ) = (
    ( ( cls ` J ) ` S ) i^i Y ) ) $=
      ( vx ctop wcel wss ccl cfv ccld wa 3adant3 wceq syl2anc eqid cuni w3a cin
      cop csubsp id topopn ssexg syl2anr stoig3 syldan syl5eqel wrex simp1 sstr
      cvv cv ancoms 3adant1 clscld ineq1 eqeq2d rcla4ev fveq2i eleq2i wb subcld
      sylancl syl5bb mpbird sscls simp3 ssin sylib clsss2 syl3anc fveq1i sseq2i
      jca stoig2 sylan2b mpbid unieqi eqcomi adantr inss1 sseq1 mpbiri ad2antll
      sseqtrd sstrd wi 3expb 3ad2antl1 ssrin syl sseq2 syl5ibrcom expr impr mpd
      com23 exp32 rexlimdv eqssd ) BIJZEDKZAEKZUAZACLMZMZABLMMZEUBZXHCIJZXLCNMZ
      JZAXLKZXJXLKXEXFXMXGXEXFOCEBUCUDMZIGXEXFEUOJZXQIJZXFXFDBJXRXEXFUEBDFUFEDB
      UGUHEBUOUIUJZUKPZXHXOXLHUPZEUBZQZHBNMZULZXHXKYEJZXLXLQZYFXHXEADKZYGXEXFXG
      UMZXFXGYIXEXGXFYIAEDUNUQURZABDFUSRXLSYDYHHXKYEYBXKQYCXLXLYBXKEUTVAVBVGXOX
      LXQNMZJZXHYFXNYLXLCXQNGVCVDXEXFYMYFVEXGHXLEBDFVFPVHVIXHAXKKZXGOXPXHYNXGXH
      XEYIYNYJYKABDFVJRXEXFXGVKZVRAXKEVLVMXLACCTZYPSVNVOXHXJYCQZHYEULZXLXJKZXHX
      JYLJZYRXHXJAXQLMZMZYLAXIUUACXQLGVCVPXHXSAXQTZKZUUBYLJXEXFXSXGXTPXHAEUUCYO
      XEXFEUUCQZXGXFXEEBTZKUUEDUUFEFVQEBVSVTPWIZAXQUUCUUCSUSRUKXEXFYTYRVEXGHXJE
      BDFVFPWAXHYQYSHYEXHYBYEJZYQYSXHUUHYQOZOZAYBKZYSUUJAXJYBXHAXJKZUUIXHXMUUDU
      ULYAUUGACUUCYPUUCCXQGWBWCVJRWDYQXJYBKZXHUUHYQUUMYCYBKYBEWEXJYCYBWFWGWHWJX
      HUUHYQUUKYSWKXHUUHOUUKYQYSXHUUHUUKYQYSWKXHUUHUUKOZOZYSYQXLYCKZUUOXKYBKZUU
      PXEXFUUNUUQXGXEUUHUUKUUQYBABDFVNWLWMXKYBEWNWOXJYCXLWPWQWRXAWSWTXBXCWTXD
      $.
      $( [15-Dec-2013] $) $( [22-Jan-2010] $)
  $}

  ${
    $d o x J $.  $d o x K $.  $d o x S $.  $d o x X $.  $d o x Y $.
    subntr.1 $e |- X = U. J $.
    subntr.2 $e |- K = ( subSp ` <. Y , J >. ) $.
    $( An interior in a subspace topology.  Willard in _General Topology_ says
       that there is no analog of ~ subcls for interiors.  In some sense, that
       is true.  (Contributed by Jeff Hankins, 23-Jan-2010.) $)
    subntr $p |- ( ( J e. Top /\ Y C_ X /\ S C_ Y ) -> ( ( int ` K ) ` S ) = (
    ( ( int ` J ) ` ( S u. ( X \ Y ) ) ) i^i Y ) ) $=
      ( vo vx ctop wcel wss cnt cfv cvv wa syl2anc syl5eqel wi syl w3a cdif cun
      cin cv wceq wrex cop csubsp fveq2i fveq1i cuni topopn ssexg ancoms stoig3
      sylan syldan 3adant3 sseq2i stoig2 sylan2b sseq2d biimp3a ntropn wb simp1
      eqid uniexg sylan2 elsubsp mpbid wo eltopss sseld adantrr 3ad2antl1 eldif
      wn simplbi2 orrd syl6 elin eleq2 elun1 syl6bir ad2antll syl5bir elun2 a1i
      expdimp jaod ex mpdd ssrdv adantr unieqi eqcomi ntrss2 unss1 sstrd simpl1
      sstr difss jctir unss sylib simprl simprr ssntr syl22anc ssrin syl5ibrcom
      3adant1 sseq1 expr com23 impr mpd exp32 rexlimdv elsubspr syl6eleqr orcom
      syl3anc elun df-or 3bitri anbi1i bitri elndif pm2.27 impcom syl5bi eqssd
      ) BJKZEDLZAELZUAZACMNZNZADEUBZUCZBMNNZEUDZYSUUAHUEZEUDZUFZHBUGZUUAUUELZYS
      UUAEBUHUINZKZUUIYSUUAAUUKMNZNZUUKAYTUUMCUUKMGUJUKYSUUKJKZAUUKULZLZUUNUUKK
      YPYQUUOYRYPYQEOKZUUOYPDBKZYQUURBDFUMYQUUSUUREDBUNUOUQZEBOUPURUSZYPYQYRUUQ
      YPYQPEUUPAYQYPEBULZLEUUPUFDUVBEFUTEBVAVBVCVDZAUUKUUPUUPVHVEQRYSYPUURUULUU
      IVFYPYQYRVGZYPYQUURYRYQYPUURYPYQDOKUURYPDUVBOFBJVIREDOUNVJUOUSHUUAEBOVKQV
      LYSUUHUUJHBYSUUFBKZUUHUUJYSUVEUUHPZPZUUFUUCLZUUJUVGUUFUUAUUBUCZUUCUVGIUUF
      UVIUVGIUEZUUFKZUVJEKZUVJUUBKZVMZUVJUVIKZUVGUVKUVJDKZUVNYPYQUVFUVKUVPSZYRY
      PUVEUVQUUHYPUVEPUUFDUVJUUFBDFVNVOVPVQUVPUVLUVMUVMUVPUVLVSUVJDEVRVTWAWBUVG
      UVKUVNUVOSUVGUVKPZUVLUVOUVMUVGUVKUVLUVOUVKUVLPUVJUUGKZUVGUVOUVJUUFEWCUUHU
      VSUVOSYSUVEUUHUVSUVJUUAKUVOUUAUUGUVJWDUVJUUAUUBWEWFWGWHWKUVMUVOSUVRUVJUUB
      UUAWIWJWLWMWNWOUVGUUAALZUVIUUCLUVGCJKZUUQUVTUVGCUUKJGYSUUOUVFUVAWPRYSUUQU
      VFUVCWPACUUPCULUUPCUUKGWQWRZWSQUUAAUUBWTTXAYSUVEUUHUVHUUJSYSUVEPUVHUUHUUJ
      YSUVEUVHUUHUUJSYSUVEUVHPZPZUUJUUHUUGUUELZUWDUUFUUDLZUWEUWDYPUUCDLZUVEUVHU
      WFYPYQYRUWCXBUWDADLZUUBDLZPZUWGUWDUWHUWIYSUWHUWCYQYRUWHYPYRYQUWHAEDXCUOXN
      ZWPDEXDZXEAUUBDXFZXGYSUVEUVHXHYSUVEUVHXIUUCBUUFDFXJXKUUFUUDEXLTUUAUUGUUEX
      OXMXPXQXRXSXTYAXSYSUWAUUQUUECKUUEALUUEUUALYSCUUKJGUVARUVCYSUUEUUKCYSYPUUR
      UUDBKZUUEUUKKUVDYPYQUURYRUUTUSYSYPUWGUWNUVDYSUWJUWGYSUWHUWIUWKUWLXEUWMXGZ
      UUCBDFVEQUUDEBOYBYEGYCYSUUEUUCEUDZAYSUUDUUCLZUUEUWPLYSYPUWGUWQUVDUWOUUCBD
      FWSQUUDUUCEXLTYSIUWPAUVJUWPKZUVMVSZUVJAKZSZUVLPZYSUWTUWRUVJUUCKZUVLPUXBUV
      JUUCEWCUXCUXAUVLUXCUWTUVMVMUVMUWTVMUXAUVJAUUBYFUWTUVMYDUVMUWTYGYHYIYJUXBU
      WTSYSUVLUXAUWTUVLUWSUXAUWTSUVJEDYKUWSUWTYLTYMWJYNWOXAACUUEUUPUWBXJXKYO $.
      $( [15-Dec-2013] $) $( [23-Jan-2010] $)
  $}


