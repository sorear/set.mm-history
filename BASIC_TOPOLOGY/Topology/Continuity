$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Continuity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Cn $.
  $c CnP $.

  $( Extend class notation with the set of continuous functions between
     topologies. $)
  ccn $a class Cn $.

  $( Extend class notation with the set of functions between topologies
     continuous at a point. $)
  ccnp $a class CnP $.

  ${
    $d j k f x y g $.
    $( Define a function on two topologies whose value is the set of continuous
       mappings from the first topology to the second.  Based on definition of
       continuous function in [Munkres] p. 102.  See ~ iscn for the predicate
       form. $)
    df-cn $a |- Cn = ( j e. Top , k e. Top |->
                  { f e. ( U. k ^m U. j ) | A. y e. k ( `' f " y ) e. j } ) $.

    $( Define a function on two topologies whose value is the set of continuous
       mappings at a specified point in the first topology.  Based on Theorem
       7.2(g) of [Munkres] p. 107. $)
    df-cnp $a |- CnP = ( j e. Top , k e. Top |-> ( x e. U. j |->
                  { f e. ( U. k ^m U. j ) | A. y e. k ( ( f ` x ) e. y ->
                    E. g e. j ( x e. g /\ ( f " g ) C_ y ) ) } ) ) $.
  $}

  ${
    $d f j k v w x y J $.  $d f k w x y K $.  $d f j k x X $.  $d f k Y $.
    cnfval.1 $e |- X = U. J $.
    cnfval.2 $e |- Y = U. K $.
    $( The set of all continuous functions from topology ` J ` to topology
       ` K ` . $)
    cnfval $p |- ( ( J e. Top /\ K e. Top ) ->
      ( J Cn K ) = { f e. ( Y ^m X ) | A. y e. K ( `' f " y ) e. J } ) $=
      ( vj vk ctop cv wcel wral cuni cmap co crab wceq unieq ccn syl6eqr oveq2d
      ccnv cima eleq2 ralbidv rabeqbidv oveq1d raleq df-cn ovex rabex ovmpt2 )
      IJCDKKBLUDALUEZILZMZAJLZNZBUROZUPOZPQZRUOCMZADNZBFEPQZRUAVCAURNZBUTEPQZRU
      PCSZUSVFBVBVGVHVAEUTPVHVACOEUPCTGUBUCVHUQVCAURUPCUOUFUGUHURDSZVFVDBVGVEVI
      UTFEPVIUTDOFURDTHUBUIVCAURDUJUHABIJUKVDBVEFEPULUMUN $.
      $( [11-Nov-2013] $) $( [17-Oct-2006] $)

    $( The function mapping the points in a topology ` J ` to the set of all
       functions from ` J ` to topology ` K ` continuous at that point. $)
    cnpfval $p |- ( ( J e. Top /\ K e. Top ) -> ( J CnP K ) =
         ( x e. X |-> { f e. ( Y ^m X ) | A. w e. K
        ( ( f ` x ) e. w -> E. v e. J ( x e. v /\ ( f " v ) C_ w ) ) } ) ) $=
      ( vj vk ctop wcel cv wral cmap co wceq cuni cfv cima wa wrex wi crab cmpt
      wss cvv ccnp topopn mptexg syl adantr unieq syl6eqr oveq2d imbi2d ralbidv
      rexeq rabeqbidv mpteq12dv oveq1d raleq mpteq2dva df-cnp ovmpt2g mpd3an3 )
      EMNZFMNZAGAOZDOZUABOZNZVKCOZNVLVOUBVMUHUCZCEUDZUEZBFPZDHGQRZUFZUGZUINZEFU
      JRWBSVIWCVJVIGENWCEGIUKAGWAEULUMUNKLEFMMAKOZTZVNVPCWDUDZUEZBLOZPZDWHTZWEQ
      RZUFZUGWBUJAGVRBWHPZDWJGQRZUFZUGUIWDESZAWEWLGWOWPWEETGWDEUOIUPZWPWIWMDWKW
      NWPWEGWJQWQUQWPWGVRBWHWPWFVQVNVPCWDEUTURUSVAVBWHFSZAGWOWAWRWOWASVKGNWRWMV
      SDWNVTWRWJHGQWRWJFTHWHFUOJUPVCVRBWHFVDVAUNVEABDCKLVFVGVH $.
      $( [11-Nov-2013] $) $( [17-Oct-2006] $)
  $}

  ${
    $d f x y F $.  $d f v w x y J $.  $d y w v f K $.  $d f v w x y P $.
    $d f v w y X $.  $d f v w y Y $.
    iscn.1 $e |- X = U. J $.
    iscn.2 $e |- Y = U. K $.
    $( The predicate " ` F ` is a continuous function from topology ` J ` to
       topology ` K ` ."  Definition of continuous function in [Munkres]
       p. 102. $)
    iscn $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Cn K ) <->
               ( F : X --> Y /\ A. y e. K ( `' F " y ) e. J ) ) ) $=
      ( vf ctop wcel wa co cv ccnv cima wral cvv cuni uniexg ccn cmap wf cnfval
      crab eleq2d wceq cnveq imaeq1d eleq1d elrab syl5eqel elmapg syl2an ancoms
      ralbidv wb anbi1d syl5bb bitrd ) CJKZDJKZLZBCDUAMZKBINZOZANZPZCKZADQZIFEU
      BMZUEZKZEFBUCZBOZVGPZCKZADQZLZVCVDVLBAICDEFGHUDUFVMBVKKZVRLVCVSVJVRIBVKVE
      BUGZVIVQADWAVHVPCWAVFVOVGVEBUHUIUJUPUKVCVTVNVRVBVAVTVNUQZVBFRKERKWBVAVBFD
      SRHDJTULVAECSRGCJTULFEBRRUMUNUOURUSUT $.
      $( [17-Oct-2006] $)

    $( The set of all functions from topology ` J ` to topology ` K ` that are
       continuous at a point ` P ` . $)
    cnpval $p |- ( ( J e. Top /\ K e. Top /\ P e. X ) -> ( ( J CnP K ) ` P ) =
         { f e. ( Y ^m X ) | A. y e. K
            ( ( f ` P ) e. y -> E. x e. J ( P e. x /\ ( f " x ) C_ y ) ) } ) $=
      ( vv ctop wcel co cfv cv wa wrex wi wral ccnp cima cmap crab wceq cnpfval
      wss cmpt fveq1d fveq2 eleq1d eleq1 anbi1d rexbidv imbi12d ralbidv rabbidv
      eqid ovex rabex fvmpt sylan9eq 3impa ) ELMZFLMZCGMZCEFUANZOZCDPZOZBPZMZCA
      PZMZVIVMUBVKUGZQZAERZSZBFTZDHGUCNZUDZUEVDVEQZVFVHCKGKPZVIOZVKMZWCVMMZVOQZ
      AERZSZBFTZDVTUDZUHZOWAWBCVGWLKBADEFGHIJUFUIKCWKWAGWLWCCUEZWJVSDVTWMWIVRBF
      WMWEVLWHVQWMWDVJVKWCCVIUJUKWMWGVPAEWMWFVNVOWCCVMULUMUNUOUPUQWLURVSDVTHGUC
      USUTVAVBVC $.
      $( [11-Nov-2013] $) $( [17-Oct-2006] $)

    $( The predicate " ` F ` is a continuous function from topology ` J ` to
       topology ` K ` at point ` P ` ."  Based on Theorem 7.2(g) of [Munkres]
       p. 107. $)
    iscnp $p |- ( ( J e. Top /\ K e. Top /\ P e. X ) ->
                   ( F e. ( ( J CnP K ) ` P ) <-> ( F : X --> Y /\ A. y e. K
          ( ( F ` P ) e. y -> E. x e. J ( P e. x /\ ( F " x ) C_ y ) ) ) ) ) $=
      ( vf ctop wcel co cfv cv cima wss wa cvv ccnp wrex wi wral cmap wf cnpval
      w3a crab eleq2d wb wceq fveq1 eleq1d imaeq1 sseq1d anbi2d rexbidv imbi12d
      ralbidv elrab uniexg syl5eqel elmapg syl2an ancoms anbi1d syl5bb 3adant3
      cuni bitrd ) ELMZFLMZCGMZUHZDCEFUANOZMDCKPZOZBPZMZCAPZMZVQWAQZVSRZSZAEUBZ
      UCZBFUDZKHGUENZUIZMZGHDUFZCDOZVSMZWBDWAQZVSRZSZAEUBZUCZBFUDZSZVOVPWJDABCK
      EFGHIJUGUJVLVMWKXAUKVNWKDWIMZWTSVLVMSZXAWHWTKDWIVQDULZWGWSBFXDVTWNWFWRXDV
      RWMVSCVQDUMUNXDWEWQAEXDWDWPWBXDWCWOVSVQDWAUOUPUQURUSUTVAXCXBWLWTVMVLXBWLU
      KZVMHTMGTMXEVLVMHFVJTJFLVBVCVLGEVJTIELVBVCHGDTTVDVEVFVGVHVIVK $.
      $( [17-Oct-2006] $)
  $}

  ${
    $d x y F $.  $d x y J $.  $d x y K $.  $d x y X $.  $d x y Y $.
    $d P x y $.
    iscnp2.1 $e |- X = U. J $.
    iscnp2.2 $e |- Y = U. K $.
    $( The predicate " ` F ` is a continuous function from topology ` J ` to
       topology ` K ` at point ` P ` ." $)
    iscnp2 $p |- ( ( J e. Top /\ K e. Top /\ P e. X ) ->
                   ( F e. ( ( J CnP K ) ` P ) <-> ( F : X --> Y /\ A. y e. K
       ( ( F ` P ) e. y -> E. x e. J ( P e. x /\ x C_ ( `' F " y ) ) ) ) ) ) $=
      ( ctop wcel cfv cv cima wss wa wrex wi wral ccnp co wf ccnv iscnp wb wfun
      w3a cdm ffun ad2antlr eltopss adantlr wceq fdm sseqtr4d funimass3 syl2anc
      anbi2d rexbidva imbi2d ralbidv pm5.32da 3ad2ant1 bitrd ) EKLZFKLZCGLZUHDC
      EFUAUBMLGHDUCZCDMBNZLZCANZLZDVLOVJPZQZAERZSZBFTZQZVIVKVMVLDUDVJOPZQZAERZS
      ZBFTZQZABCDEFGHIJUEVFVGVSWEUFVHVFVIVRWDVFVIQZVQWCBFWFVPWBVKWFVOWAAEWFVLEL
      ZQZVNVTVMWHDUGZVLDUIZPVNVTUFVIWIVFWGGHDUJUKWHVLGWJVFWGVLGPVIVLEGIULUMVIWJ
      GUNVFWGGHDUOUKUPVLVJDUQURUSUTVAVBVCVDVE $.
      $( [21-Jun-2008] $) $( [15-May-2007] $)

    $( A continuous function is a mapping.  (Contributed by FL,
       15-Dec-2006.) $)
    cnf $p |- ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) ) ->
                F : X --> Y ) $=
      ( vx ctop wcel ccn co wf wa ccnv cv cima wral iscn simprbda 3impa ) BIJZC
      IJZABCKLJZDEAMZUBUCNUDUEAOHPQBJHCRHABCDEFGSTUA $.
      $( [8-Dec-2006] $)

    $( A continuous function at point ` P ` is a mapping.  (Contributed by FL,
       31-Dec-2006.) $)
    cnpf $p |- ( ( ( J e. Top /\ K e. Top /\ P e. X ) /\
        F e. ( ( J CnP K ) ` P ) ) -> F : X --> Y ) $=
      ( vx vy ctop wcel w3a ccnp co cfv wf cv cima wss wa wrex wi wral simprbda
      iscnp ) CKLDKLAELMBACDNOPLEFBQABPIRZLAJRZLBUHSUGTUAJCUBUCIDUDJIABCDEFGHUF
      UE $.
      $( [17-Nov-2006] $)

    $( The value of a continuous function from ` J ` to ` K ` at point ` P `
       belongs to the underlying set of topology ` K ` .  (Contributed by FL,
       31-Dec-2006.) $)
    cnpcl $p |- ( ( ( J e. Top /\ K e. Top /\ P e. X ) /\
        ( F e. ( ( J CnP K ) ` P ) /\ A e. X ) ) -> ( F ` A ) e. Y ) $=
      ( ctop wcel w3a ccnp co cfv wa wf cnpf adantrr simprr ffvelrn syl2anc ) D
      JKEJKBFKLZCBDEMNOKZAFKZPPFGCQZUEACOGKUCUDUFUEBCDEFGHIRSUCUDUETFGACUAUB $.
      $( [27-Dec-2006] $)
  $}

  ${
    $d x y A $.  $d F y x $.  $d J x y $.  $d K y $.  $d P x y $.  $d X y $.
    cnpimaex.1 $e |- X = U. J $.
    $( Property of a function continuous at a point.  (Contributed by FL,
       31-Dec-2006.) $)
    cnpimaex $p |- ( ( ( J e. Top /\ K e. Top /\ P e. X ) /\ (
                   F e. ( ( J CnP K ) ` P ) /\ A e. K /\ ( F ` P ) e. A ) ) ->
                    E. x e. J ( P e. x /\ ( F " x ) C_ A ) ) $=
      ( vy ctop wcel w3a ccnp co cfv cv wss wa wrex wi cima wral iscnp simplbda
      cuni wf eqid wceq eleq2 sseq2 anbi2d rexbidv imbi12d rcla4cv syl6 3imp2
      ex ) EJKFJKCGKLZDCEFMNOKZBFKZCDOZBKZCAPZKZDVCUAZBQZRZAESZURUSVAIPZKZVDVEV
      IQZRZAESZTZIFUBZUTVBVHTZTURUSVOURUSGFUEZDUFVOAICDEFGVQHVQUGUCUDUQVNVPIBFV
      IBUHZVJVBVMVHVIBVAUIVRVLVGAEVRVKVFVDVIBVEUJUKULUMUNUOUP $.
      $( [22-Dec-2006] $)

    $( A restricted identity function is a continuous function.  (Contributed
       by FL, 31-Dec-2006.) $)
    idcn $p |- ( J e. Top -> ( _I |` X ) e. ( J Cn J ) ) $=
      ( vy ctop wcel cid cres ccnv cima wa wb wss a1i wfun wceq ax-mp eqtrd syl
      cnvi ccn co wf cv wral iscn anidms wfn rnresi eqimssi fnresi jctil sylibr
      df-f funi eqcomi funeq sylib funcnvres imai reseq2d reseq1 syl6eq imaeq1d
      crn eltopss resiima simpr eqeltrd ralrimiva mpbir2and ) AEFZGBHZAAUAUBFZB
      BVMUCZVMIZDUDZJZAFZDAUEZVLVNVOVTKLDVMAABBCCUFUGVLVMBUHZVMVEZBMZKVOVLWCWAW
      CVLWBBBUIUJNBUKULBBVMUNUMVLVSDAVLVQAFZKZVRVQAWEVRVMVQJZVQWEVPVMVQWEVPGIZB
      HZVMWEWGOZVPWHPWEGOZWIWJWEUONGWGPWJWILWGGTUPGWGUQQURWIVPWGGBJZHWHBGUSWIWK
      BWGWKBPWIBUTNVARSWGGPWHVMPTWGGBVBQVCVDWEVQBMWFVQPVQABCVFBVQVGSRVLWDVHVIVJ
      VK $.
      $( [27-Dec-2006] $)
  $}

  ${
    $d g o y A $.  $d g o y F $.  $d g o y J $.  $d g o y K $.  $d g o y X $.
    $d g o y Y $.
    cnpnei.1 $e |- X = U. J $.
    cnpnei.2 $e |- Y = U. K $.
    $( A condition for continuity at a point in terms of neighborhoods.
       (Contributed by Jeff Hankins, 7-Sep-2009.) $)
    cnpnei $p |- ( ( ( J e. Top /\ K e. Top /\ F : X --> Y ) /\ A e. X ) -> (
    F e. ( ( J CnP K ) ` A ) <-> A. y e. ( ( nei ` K ) ` { ( F ` A ) } ) ( `' F
    " y ) e. ( ( nei ` J ) ` { A } ) ) ) $=
      ( vo vg wcel wa cfv wss wrex wb ad2antrr wi adantlr ctop wf w3a ccnp ccnv
      cima csn cnei wral isneip 3ad2antl1 adantr cdm cnvimass syl5sseq 3ad2ant3
      co cv neii2 3ad2antl2 ad2ant2rl simp1 simp2 simpr 3jca simpll simprl fvex
      fdm snss biimpri ad2antll adantll cnpimaex sstr2 com12 ad2antlr wfun ffun
      syl2anc sseq2d mpbird 3adantl2 funimass3 sylibd anim2d reximdva mpd exp32
      eltopss rexlimdv mpbir2and ralrimdv iscnp 3adantl3 simpll3 opnneip imaeq2
      3expa wceq eleq1d rcla4v syl 3com23 3expb 3ad2ant1 snssg adantl ad3antrrr
      ex biimpar syldan anbi12d biimprd 3syld com24 imp ralrimiv impbid ) DUALZ
      EUALZFGCUBZUCZBFLZMZCBDEUDUQNLZCUEZAURZUFZBUGZDUHNNZLZABCNZUGZEUHNNZUIZYE
      YFYLAYOYEYFYHYOLZYLYEYFYQMZMZYLYIFOZBJURZLZUUAYIOZMZJDPZYEYLYTUUEMQZYRXTY
      AYDUUFYBBJDYIFHUJUKULYCYTYDYRYBXTYTYAYBCUMZYIFCYHUNFGCVIZUOUPRYSYNKURZOZU
      UIYHOZMZKEPZUUEYCYQUUMYDYFYAXTYQUUMYBYNKEYHUSUTVAYSUULUUEKEYSUUIELZUULUUE
      YSUUNUULMZMZUUBCUUAUFZUUIOZMZJDPZUUEUUPXTYAYDUCZYFUUNYMUUILZUCZUUTYEUVAYR
      UUOYEXTYAYDYCXTYDXTYAYBVBULYCYAYDXTYAYBVCULYCYDVDVERYRUUOUVCYEYRUUOMYFUUN
      UVBYFYQUUOVFYRUUNUULVGUULUVBYRUUNUUJUVBUUKUVBUUJYMUUIBCVHVJVKULVLVEVMJUUI
      BCDEFHVNVTUUPUUSUUDJDUUPUUADLZMZUURUUCUUBUVEUURUUQYHOZUUCUUOUURUVFSZYSUVD
      UUKUVGUUNUUJUURUUKUVFUUQUUIYHVOVPVLVQUVECVRZUUAUUGOZUVFUUCQYSUVHUUOUVDYCU
      VHYDYRYBXTUVHYAFGCVSUPZRRYSUVDUVIUUOYEUVDUVIYRYCUVDUVIYDXTYBUVDUVIYAXTYBM
      UVDMUVIUUAFOZXTUVDUVKYBUUADFHWJTYBUVIUVKQXTUVDYBUUGFUUAUUHWAVQWBWCTTTUUAY
      HCWDVTWEWFWGWHWIWKWHWLWIWMYEYPYFYEYPMZYFYBYMUUALZBUUILZCUUIUFUUAOZMZKDPZS
      ZJEUIZYEYFYBUVSMQZYPXTYAYDUVTYBXTYAYDUVTKJBCDEFGHIWNWSWOULXTYAYBYDYPWPUVL
      UVRJEYEYPUUAELZUVRSYEUVMUWAYPUVQYEUVMUWAYPUVQSYEUVMUWAMZMZYPYGUUAUFZYKLZY
      JUUIOZUUIUWDOZMZKDPZUVQYCUWBYPUWESZYDYAXTUWBUWJYBYAUVMUWAUWJYAUWAUVMUWJYA
      UWAUVMUCUUAYOLUWJYMEUUAWQYLUWEAUUAYOYHUUAWTYIUWDYKYHUUAYGWRXAXBXCXDXEUTTY
      CUWEUWISZYDUWBXTYAUWKYBXTUWEUWIYJKDUWDUSXJXFRUWCUWHUVPKDUWCUUIDLZMZUVPUWH
      UWMUVNUWFUVOUWGYEUVNUWFQZUWBUWLYDUWNYCBUUIFXGXHRUWMUVHUUIUUGOZUVOUWGQYCUV
      HYDUWBUWLUVJXIYEUWLUWOUWBYCUWLUWOYDYCUWLUUIFOZUWOXTYAUWLUWPYBUUIDFHWJUKYC
      UWOUWPYBXTUWOUWPQYAYBUUGFUUIUUHWAUPXKXLTTUUIUUACWDVTXMXNWGXOWIXPXQXRWLXJX
      S $.
      $( [7-Sep-2009] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x J $.  $d x K $.
    $( An open subset of the codomain of a continuous function has an open
       preimage.  (Contributed by FL, 15-Dec-2006.) $)
    cnima $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) ) /\ A e. K ) ->
                 ( `' F " A ) e. J ) $=
      ( vx ctop wcel ccn co w3a ccnv cima cv wral wi wa cuni wf eqid iscn 3impa
      simplbda wceq imaeq2 eleq1d rcla4cv syl imp ) CFGZDFGZBCDHIGZJZADGZBKZALZ
      CGZULUNEMZLZCGZEDNZUMUPOUIUJUKUTUIUJPUKCQZDQZBRUTEBCDVAVBVASVBSTUBUAUSUPE
      ADUQAUCURUOCUQAUNUDUEUFUGUH $.
      $( [8-Dec-2006] $)
  $}

  ${
    $d x F $.  $d x G $.  $d x J $.  $d x K $.  $d x L $.
    $( The composition of two continuous functions is a continuous function.
       (Contributed by FL, 15-Dec-2006.) $)
    cnco $p |- ( ( ( J e. Top /\ K e. Top /\ L e. Top ) /\ ( F e. ( J Cn K )
                  /\ G e. ( K Cn L ) ) ) -> ( G o. F ) e. ( J Cn L ) ) $=
      ( vx ctop wcel w3a ccn co wa ccom cuni wf ccnv cima eqid cnf 3expa adantr
      cv wral wb iscn 3adant2 3adantl1 3adantl3 fco syl2an ancoms anandis cnvco
      imaeq1i imaco eqtri simpll1 simpll2 simpl2 simpl3 simprr 3jca cnima sylan
      simplrl syl31anc syl5eqel ralrimiva mpbir2and ) CGHZDGHZEGHZIZACDJKHZBDEJ
      KHZLZLZBAMZCEJKHZCNZENZVROZVRPZFUBZQZCHZFEUCZVMVSWBWGLUDZVPVJVLWHVKFVRCEV
      TWAVTRZWARZUEUFUAVMVNVOWBVMVOLZVMVNLZWBWKDNZWABOZVTWMAOZWBWLVKVLVOWNVJVKV
      LVOWNBDEWMWAWMRZWJSTUGVJVKVNWOVLVJVKVNWOACDVTWMWIWPSTUHVTWMWABAUIUJUKULVQ
      WFFEVQWDEHZLZWEAPZBPZWDQZQZCWEWSWTMZWDQXBWCXCWDBAUMUNWSWTWDUOUPWRVJVKVNXA
      DHZXBCHVJVKVLVPWQUQVJVKVLVPWQURVMVNVOWQVEVQVKVLVOIWQXDVQVKVLVOVJVKVLVPUSV
      JVKVLVPUTVMVNVOVAVBWDBDEVCVDXAACDVCVFVGVHVI $.
      $( [8-Dec-2006] $)
  $}

  ${
    $d x y z F $.  $d x y z G $.  $d x y z J $.  $d x y z K $.  $d x y z L $.
    $d x y z P $.  $d x y z X $.
    cnpco.1 $e |- X = U. J $.
    $( MAY BE REVISED - this proof needs to be shortened significantly $)
    $( The composition of two continuous functions at point ` P ` is a
       continuous function at point ` P ` .  Bourbaki TG I.9.  (Contributed by
       FL, 31-Dec-2006.) $)
    cnpco $p |- ( ( ( J e. Top /\ L e. Top /\ P e. X ) /\ ( K e. Top
        /\ F e. ( ( J CnP K ) ` P ) /\ G e. ( ( K CnP L ) ` ( F ` P ) ) ) )
        -> ( G o. F ) e. ( ( J CnP L ) ` P ) ) $=
      ( vz vx vy wcel w3a cfv wa wss wrex wi com13 ad2antrr ctop ccnp ccom cuni
      co wf cv cima wral eqid iscnp adantr simpr1 simpl2 eleq2i pm3.2 com34 a1i
      wb 3exp 3imp sylbi com12 a1d 3imp1 cnpf syl simp3 syl6eleq ffvelrn simpr3
      syl2anc syl31anc com14 fco simpl1 simpl3 simplr2 simplr cnpimaex syl33anc
      simpr ad3antrrr ex anim1d r19.41v syl6ibr imass2 sstr sylan anim2i reximi
      anassrs syl6 reximdva mpd c0 wne 0opn ne0i 3ad2ant1 adantl r19.9rzv exp31
      mpbird ralrimiv wfun cdm wceq 3jca jca ffun 3syl fdm eqcomd eleqtrd eleq1
      fvco biimpd eqcomi sseq1i biimpi anim2d reximdv imim12d ralimdv mpbir2and
      imaco ) DUALZFUALZAGLZMZEUALZBADEUBUENLZCABNZEFUBUENLZMZOZCBUCZADFUBUENLZ
      GFUDZYSUFZAYSNZIUGZLZAJUGZLZYSUUFUHZUUDPZOZJDQZRZIFUIZYLYTUUBUUMOUSYQJIAY
      SDFGUUAHUUAUJZUKULYREUDZUUACUFZGUUOBUFZUUBYRYMYJYOUUOLZYPUUPYLYMYNYPUMZYI
      YJYKYQUNZYRDUDZUUOBUFZAUVALZUURYRYIYMUVCMZYNOZUVBYIYJYKYQUVEYIYKYQUVERZRY
      JYKYIUVFYKUVCYIUVFRGUVAAHUOYQYIUVCUVEYMYNYPYIUVCUVERZRZYPYNYMUVHYNYMUVHRR
      YPYIYMYNUVGYIYMUVCYNUVEYIYMUVCYNUVERUVDYNUPUTUQSURSVASVBVCVDVEABDEUVAUUOU
      VAUJUUOUJZVFZVGYLUVCYQYLAGUVAYIYJYKVHHVIULUVAUUOABVJZVLZYLYMYNYPVKZYOCEFU
      UOUUAUVIUUNVFZVMYRYIYMYKMZYNOZUUQYIYJYKYQUVPYIYKYQUVPRRYJYQYKYIUVPYMYNYPY
      KYIUVPRRZYPYNYMUVQYNYMUVQRRYPYIYMYKYNUVPYIYMYKYNUVPRUVOYNUPUTVNURSVASVDVE
      ZABDEGUUOHUVIVFZVGGUUOUUACBVOVLYRYOCNZUUDLZUUGCBUUFUHZUHZUUDPZOZJDQZRZIFU
      IUUMYRUWGIFYRUUDFLZUWAUWFYRUWHOZUWAOZUWFUWFKEQZUWJYOKUGZLZCUWLUHZUUDPZOZK
      EQZUWKUWJYMYJUURYPUWHUWAUWQYRYMUWHUWAUUSTZYRYJUWHUWAUUTTUWJUVBUVCUURUWJYI
      YMUVCYNUVBYRYIUWHUWAYIYJYKYQVPTZUWRUWJAGUVAYRYKUWHUWAYIYJYKYQVQZTZHVIZUWI
      YNUWAYMYNYPYLUWHVRZULUVJVMUXBUVKVLYRYPUWHUWAUVMTYRUWHUWAVSUWIUWAWBKUUDYOC
      EFUUOUVIVTWAUWJUWPUWFKEUWJUWLELZOZUWPUUGUWBUWLPZOZUWOOZJDQZUWFUXEUWPUXGJD
      QZUWOOUXIUXEUWMUXJUWOUXEUWMUXJUXEUWMOYIYMYKYNUXDUWMUXJUWJYIUXDUWMUWSTUWJY
      MUXDUWMUWRTUWJYKUXDUWMUXATUWIYNUWAUXDUWMUXCWCUWJUXDUWMVSUXEUWMWBJUWLABDEG
      HVTWAWDWEUXGUWOJDWFWGUXHUWEJDUUGUXFUWOUWEUXFUWOOUWDUUGUXFUWCUWNPUWOUWDUWB
      UWLCWHUWCUWNUUDWIWJWKWMWLWNWOWPUWJEWQWRZUWFUWKUSYRUXKUWHUWAYQUXKYLYMYNUXK
      YPYMWQELUXKEWSEWQWTVGXAXBTUWFKEXCVGXEXDXFYRUWGUULIFYRUUEUWAUWFUUKYRUUEUWA
      YRCXGZBXGZABXHZLZMUUCUVTXIUUEUWAUSYRUXLUXMUXOYRYMYJUURMZYPOUUPUXLYRUXPYPY
      RYMYJUURUUSUUTUVLXJUVMXKUVNUUOUUACXLXMYRUVPUUQUXMUVRUVSGUUOBXLXMYRAGUXNUW
      TYRUVPUUQGUXNXIUVRUVSUUQUXNGGUUOBXNXOXMXPXJACBXRUUCUVTUUDXQXMXSYRUWEUUJJD
      YRUWDUUIUUGUWDUUIRYRUWDUUIUWCUUHUUDUUHUWCCBUUFYHXTYAYBURYCYDYEYFWPYG $.
      $( [16-Nov-2006] $)
  $}

  ${
    $d F x y $.  $d J x y $.  $d K x y $.  $d X x y $.  $d Y x y $.
    iscncl.1 $e |- X = U. J $.
    iscncl.2 $e |- Y = U. K $.
    $( A definition of a continuous function using closed sets.  Bourbaki TG
       I.9 Th. 1 (d).  (Contributed by FL, 30-Jan-2007.) $)
    iscncl $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Cn K ) <->
    ( F : X --> Y /\ A. y e. ( Clsd ` K ) ( `' F " y ) e. ( Clsd ` J ) ) ) ) $=
      ( vx ctop wcel wa ccnv cv cima cdif wi wceq imp41 adantlr co wf wral ccld
      ccn cfv iscn fimacnv eqcomd difeq1d wfun ffun funcnvcnv imadif 3syl a1i12
      eqtr4d com13 simplr cldopn imaeq2 rcla4cva syl2anc adantllr eqeltrd exp41
      a1i eleq1d adantl wb wss cdm cnvimass fdm syl5sseq iscld2 sylan2 ad2antrr
      mpbird ralrimiva opncld isopn2 impbida pm5.32da bitrd ) CJKZDJKZLZBCDUEUA
      KEFBUBZBMZINZOZCKZIDUCZLWIWJANZOZCUDUFZKZADUDUFZUCZLIBCDEFGHUGWHWIWNWTWHW
      ILZWNWTXAWNLZWRAWSXBWOWSKZLWREWPPZCKZWHWIWNXCXEWGWIWNXCXEQQQWFWGWIWNXCXEW
      GWILZWNLXCLXDWJFWOPZOZCWGWIWNXCXDXHRZWIWNXCXIQQQWGXCWNWIXIXCWNWIXIQWIXDWJ
      FOZWPPZXHWIEXJWPWIXJEEFBUHUIZUJWIBUKZWJMUKZXHXKREFBULZBUMZFWOWJUNUOUQUPUR
      VGSWGWNXCXHCKZWIWGWNLXCLWNXGDKZXQWGWNXCUSWGXCXRWNWODFHUTTWMXQIXGDWKXGRWLX
      HCWKXGWJVAVHVBVCVDVEVFVISXAWRXEVJZWNXCWFWIXSWGWIWFWPEVKXSWIBVLZWPEBWOVMEF
      BVNZVOWPCEGVPVQTVRVSVTXAWTLZWMIDYBWKDKZLWMEWLPZWQKZWHWIWTYCYEWGWIWTYCYEQQ
      QWFWGWIWTYCYEXFWTLYCLYDWJFWKPZOZWQWGWIWTYCYDYGRZWIWTYCYHQQQWGYCWTWIYHYCWT
      WIYHQWIYDXJWLPZYGWIEXJWLXLUJWIXMXNYGYIRXOXPFWKWJUNUOUQUPURVGSWGWTYCYGWQKZ
      WIWGWTLYCLWTYFWSKZYJWGWTYCUSWGYCYKWTWKDFHWATWRYJAYFWSWOYFRWPYGWQWOYFWJVAV
      HVBVCVDVEVFVISXAWMYEVJZWTYCWFWIYLWGWIWFWLEVKYLWIXTWLEBWKVMYAVOWLCEGWBVQTV
      RVSVTWCWDWE $.
      $( [19-Nov-2006] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x J $.  $d x K $.
    $( A closed subset of the codomain of a continuous function has a closed
       preimage. $)
    cnclima $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) ) /\
                A e. ( Clsd ` K ) ) -> ( `' F " A ) e. ( Clsd ` J ) ) $=
      ( vx ctop wcel ccn co w3a ccld cfv ccnv cima cv wral wi wa cuni eqid wceq
      wf iscncl simplbda 3impa imaeq2 eleq1d rcla4cv syl imp ) CFGZDFGZBCDHIGZJ
      ZADKLZGZBMZANZCKLZGZUNUQEOZNZUSGZEUOPZUPUTQUKULUMVDUKULRUMCSZDSZBUBVDEBCD
      VEVFVETVFTUCUDUEVCUTEAUOVAAUAVBURUSVAAUQUFUGUHUIUJ $.
      $( [15-Mar-2007] $)
  $}

  ${
    $d J f x y $.  $d K f y $.  $d X f y $.  $d Y y $.  $d P f x y $.
    cnsscnp.1 $e |- X = U. J $.
    cnsscnp.2 $e |- Y = U. K $.
    $( The set of continuous functions is a subset of the set of continuous
       functions at a point.  (Contributed by Raph Levien, 21-Oct-2006.) $)
    cnsscnp $p |- ( ( J e. Top /\ K e. Top /\ P e. X ) ->
                  ( J Cn K ) C_ ( ( J CnP K ) ` P ) ) $=
      ( vf vy vx ctop wcel co cfv cv wi wa cima wral wss w3a ccn ccnp ccnv wrex
      wf cdm wfun fdm eleq2d biimpar ffun adantr risset fvimacnv eleq2 biimprcd
      wceq syl6bi ex com4r com3r imp32 crn cin imaeq2 funimacnv sylan9eq sseq1d
      inss1 mpbiri adantl jctird exp32 com24 imp31 reximdv syl5bi exp31 ralimdv
      com4t imp syl2anc expcom imdistand iscn iscnp 3expa 3imtr4d 3impa ssrdv
      wb ) BKLZCKLZADLZUAHBCUBMZABCUCMNZWMWNWOHOZWPLZWRWQLZPWMWNQZWOQDEWRUFZWRU
      DIOZRZBLZICSZQZXBAWRNXCLZAJOZLZWRXIRZXCTZQZJBUEZPZICSZQZWSWTWOXGXQPXAWOXB
      XFXPXBWOXFXPPZXBWOQAWRUGZLZWRUHZXRXBXTWOXBXSDADEWRUIUJUKXBYAWODEWRULUMXTY
      AQXEXOICXTYAXEXOPYAXEXTXOXTXHYAXEXNXTXHYAXEXNPXEXIXDURZJBUEXTXHQYAQZXNJXD
      BUNYCYBXMJBXTXHYAYBXMPXTYBYAXHXMXTYBYAXHXMPXTYBYAQZQXHXJXLXTYBYAXHXJPZYBY
      AXTYEYAXTXHYBXJYAXTXHYBXJPZPYAXTQXHAXDLZYFAXCWRUOYBXJYGXIXDAUPUQUSUTVAVBV
      CYDXLXTYDXLXCWRVDZVEZXCTXCYHVJYDXKYIXCYBYAXKWRXDRYIXIXDWRVFXCWRVGVHVIVKVL
      VMVNVOVPVQVRVSWAVBWBVTWCWDWEVLXAWSXGWLWOIWRBCDEFGWFUMWMWNWOWTXQWLJIAWRBCD
      EFGWGWHWIWJWK $.
      $( [18-Oct-2006] $)

    $( A continuous function is continuous at all points.  One direction of
       Theorem 7.2(g) of [Munkres] p. 107.  (Contributed by Raph Levien,
       20-Nov-2006.) $)
    cncnpi $p |- ( ( ( J e. Top /\ K e. Top ) /\
             ( F e. ( J Cn K ) /\ A e. X ) ) -> F e. ( ( J CnP K ) ` A ) ) $=
      ( ctop wcel wa ccn co ccnp cfv wi w3a cnsscnp sseld 3expia com23 imp32 )
      CIJZDIJZKZBCDLMZJZAEJZBACDNMOZJZUEUHUGUJUCUDUHUGUJPUCUDUHQUFUIBACDEFGHRST
      UAUB $.
      $( [20-Nov-2006] $)
  $}

  ${
    $d x B $.
    $( MAY BE REVISED - use iscnp2 instead of iscnp to shorten cncnplem1 thru
       cncnplem4 ?  Or prove cncnp directly? $)
    $( Lemma for ~ cncnp2 . $)
    cncnplem1 $p |- U. { x e. A | ( ph /\ x C_ B ) } C_ B $=
      ( cv wss wa crab cuni cpw wcel cab simprr ss2abi df-rab df-pw uniss ax-mp
      3sstr4i unipw sseqtri ) ABEZDFZGZBCHZIZDJZIZDUEUGFUFUHFUBCKZUDGZBLUCBLUEU
      GUJUCBUIAUCMNUDBCOBDPSUEUGQRDTUA $.
      $( [20-Nov-2006] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d X x $.
    $( Lemma for ~ cncnp2 . $)
    cncnplem2 $p |- ( A. x ( x e. A -> x e. B ) -> A C_ U_ x e. A B ) $=
      ( vy cv wcel wi wal ciun wrex ax-17 hba1 hbre1 hbim wceq wa eleq1 biimpar
      vex com12 adantr ax-4 syl6bir biimpd syl6d imp31 ra4e syl2anc exp31 eliun
      vtoclef syl6ibr ssrdv ) AEZBFZUNCFZGZAHZDBABCIZDEZBFZURUTUSFZVAURUTCFZABJ
      ZVBVAURVDGZGAUTVAVEAVAAKURVDAUQALVCABMNNDSUNUTOZVAURVDVFVAPZURPUOVCVDVGUO
      URVFUOVAUNUTBQZRUAVFVAURVCVFVAURUPVCVFVAUOURUPGVHURUOUPUQAUBTUCVFUPVCUNUT
      CQUDUEUFVCABUGUHUIUKAUTBCUJULTUM $.
      $( [20-Nov-2006] $)

    $( Lemma for ~ cncnp2 . $)
    cncnplem3 $p |- ( A C_ X -> ( A. x e. X ( x e. A -> x e. B ) ->
                    A C_ U_ x e. A B ) ) $=
      ( wss cv wcel wi wral wal ciun wa ssel imbi1d impexp syl6bb albidv df-ral
      pm4.71rd syl6bbr cncnplem2 syl6bir ) BDEZAFZBGZUDCGZHZADIZUGAJZBABCKEUCUI
      UDDGZUGHZAJUHUCUGUKAUCUGUJUELZUFHUKUCUEULUFUCUEUJBDUDMSNUJUEUFOPQUGADRTAB
      CUAUB $.
      $( [20-Nov-2006] $)
  $}

  ${
    $d J x z $.  $d F x z $.  $d X x z $.  $d Y x z $.  $d x y z $.
    cncnplem4.1 $e |- X = U. J $.
    $( Lemma for ~ cncnp2 . $)
    cncnplem4 $p |- ( J e. Top -> ( ( F : X --> Y /\ A. x e. X ( ( F ` x ) e. y
      -> E. z e. J ( x e. z /\ ( F " z ) C_ y ) ) ) -> ( `' F " y ) e. J ) ) $=
      ( wcel cv cima wss wa cuni wi wral wb ex imp adantr ctop ccnv crab wf cfv
      ciun wrex ssrab2 uniopn mpan2 a1d ralrimiv iunopn wceq cdm elssuni syl6eq
      mpdan fdm sseq2d syl5ibr wfun ffun funimass3 sylan anbi2d rabbidva unieqd
      syld iuneq2dv iunss cncnplem1 a1i mprgbir cnvimass mpbii fvimacnv biimprd
      sseli pm4.24 anbi1i anass syl6bb rexbidva elunirab syl6bbr biimpd imim12d
      bitri com14 com13 ralimdv cncnplem3 sylsyld eqssd eqtrd eleq1d syl5ibcom
      mpd ) EUAIZADUBBJZKZAJZCJZIZDXDKXALZMZCEUCZNZUFZEIZFGDUDZXCDUEXAIZXGCEUGZ
      OZAFPZMZXBEIWTXIEIZAXBPXKWTXRAXBWTXRXCXBIZWTXHELXRXGCEUHXHEUIUJUKULAXBXIE
      UMURXQXJXBEXQXJAXBXEXDXBLZMZCEUCZNZUFZXBXLXJYDUNXPXLAXBXIYCXLXIYCUNXSXLXH
      YBXLXGYACEXLXDEIZMXFXTXEXLYEXFXTQZXLYEXDDUOZLZYFYEYHXLXDENZLXDEUPXLYGYIXD
      XLYGFYIFGDUSZHUQUTVAZXLYHYFXLDVBZYHYFFGDVCZXDXADVDVEZRVISVFVGVHTVJTXQYDXB
      YDXBLZXQYOYCXBLZAXBAXBYCXBVKYPXSXECEXBVLVMVNVMXLXPXBYDLZXLXBFLZXPXSXCYCIZ
      OZAFPYQXLXBYGLYRDXAVOZXLYGFXBYJUTVPXLXOYTAFXSXOXLYSXSXCYGIZXOXLYSOOXBYGXC
      UUAVSXLUUBXOXSYSXLUUBXOYTOXLUUBMZXSXMXNYSUUCXMXSXLYLUUBXMXSQYMXCXADVQVEVR
      XLXNYSOUUBXLXNYSXLXNXEYAMZCEUGYSXLXGUUDCEXLYEXGUUDQZXLYEYHUUEYKXLYHUUEXLY
      HMZXGYAUUDUUFXFXTXEYNVFYAXEXEMZXTMUUDXEUUGXTXEVTWAXEXEXTWBWIWCRVISWDYACXC
      EWEWFWGTWHRWJWSWKWLAXBYCFWMWNSWOWPWQWR $.
      $( [20-Nov-2006] $)
  $}

  ${
    $d J x y z $.  $d K x y $.  $d F x y z $.  $d X x y z $.  $d Y x y z $.
    cncnp.1 $e |- X = U. J $.
    cncnp.2 $e |- Y = U. K $.
    $( A continuous function is continuous at all points.  Theorem 7.2(g) of
       [Munkres] p. 107. $)
    cncnp $p |- ( ( J e. Top /\ K e. Top /\ F : X --> Y ) ->
                ( F e. ( J Cn K ) <-> A. x e. X F e. ( ( J CnP K ) ` x ) ) ) $=
      ( vy vz ctop wcel w3a co cv cfv wral wi wa 3adant3 ccn ccnp cnsscnp sseld
      wf 3expia com23 ralrimdv cima wss wrex ccnv simpr ralimi anim2i ex ralcom
      anbi2i cncnplem4 expdimp ralimdv imdistand sylan9r 3adant2 wb iscnp 3expa
      syl5bi ralbidva iscn imbi12d mpbird impbid ) CKLZDKLZEFBUEZMZBCDUANZLZBAO
      ZCDUBNPZLZAEQZVNVOVSWCRVPVNVOSZVSWBAEWDVTELZVSWBVNVOWEVSWBRVNVOWEMVRWABVT
      CDEFGHUCUDUFUGUHTVQWCVSRZVPVTBPIOZLVTJOZLBWHUIWGUJSJCUKRZIDQZSZAEQZVPBULW
      GUICLZIDQZSZRZVNVPWPVOVPWLVPWJAEQZSZVNWOVPWLWRWLWQVPWKWJAEVPWJUMUNUOUPWRV
      PWIAEQZIDQZSVNWOWQWTVPWIAIEDUQURVNVPWTWNVNVPWTWNRVNVPSWSWMIDVNVPWSWMAIJBC
      EFGUSUTVAUPVBVHVCVDVNVOWFWPVEVPWDWCWLVSWOWDWBWKAEVNVOWEWBWKVEJIVTBCDEFGHV
      FVGVIIBCDEFGHVJVKTVLVM $.
      $( [15-May-2007] $)

    $( A continuous function is continuous at all points.  Theorem 7.2(g) of
       [Munkres] p. 107.  (Contributed by Raph Levien, 20-Nov-2006.) $)
    cncnp2 $p |- ( ( J e. Top /\ K e. Top /\ X =/= (/) ) ->
                ( F e. ( J Cn K ) <-> A. x e. X F e. ( ( J CnP K ) ` x ) ) ) $=
      ( ctop wcel c0 wne w3a ccn co wral wa wb pm4.71rd 3expa wf cv ccnp 3expia
      cfv cncnp pm5.32da bitrd 3adant3 wi cnpf ralbidva r19.28zv sylan9bb 3impa
      cnf ex bitr4d ) CIJZDIJZEKLZMBCDNOJZEFBUAZBAUBZCDUCOUEJZAEPZQZVFUSUTVBVGR
      VAUSUTQZVBVCVBQVGVHVBVCUSUTVBVCBCDEFGHUPUDSVHVCVBVFUSUTVCVBVFRABCDEFGHUFT
      UGUHUIUSUTVAVFVGRVHVFVCVEQZAEPVAVGVHVEVIAEVHVDEJZQVEVCUSUTVJVEVCUJUSUTVJM
      VEVCVDBCDEFGHUKUQTSULVCVEAEUMUNUOUR $.
      $( [20-Nov-2006] $)
  $}

  ${
    $d J x $.  $d B x $.  $d Y x $.  $d F x $.  $d X x $.  $d K x $.
    cnconst.1 $e |- X = U. J $.
    cnconst.2 $e |- Y = U. K $.
    $( A constant function is continuous.  (Contributed by FL, 25-Jan-2007.) $)
    cnconst $p |- ( ( ( J e. Top /\ K e. Top ) /\
      ( B e. Y /\ F : X --> { B } ) ) -> F e. ( J Cn K ) ) $=
      ( vx ctop wcel wa cima wss wi c0 wceq syl5 3ad2ant1 adantr csn wf co ccnv
      ccn cv wral simprr snssi ad2antrl fss syl2anc w3a cnveq cnv0 mpan2 imaeq1
      eqtr 0ima eqcom 0opn eleq1 syl5ibcom syl5bi crn imassrn cdm dfdm4 syl5eqr
      wn fdm syl5sseq eqcomd imadmrn eqtr4i a1i df-rn wfo foconst sylan2br forn
      wne df-ne imaeq2d 3eqtrd 3adant2 imass2 3ad2ant2 eqsstrd eqssd 3exp 3imp1
      syl topopn eqeltrd ex pm2.61d a1d imp32 simpr incom disjsn biimpri syl5eq
      cin fimacnvdisj syl2an adantll ad2antrr ralrimiv jca adantlr iscn mpbird
      wb ) CJKZDJKZLZAFKZEAUAZBUBZLZLBCDUEUCKZEFBUBZBUDZIUFZMZCKZIDUGZLZXPYBYJX
      QXPYBLZYDYIYKYAXTFNZYDXPXSYAUHXSYLXPYAAFUIUJEXTFBUKULYKYHIDYKYHYFDKYKAYFK
      ZYHXPXSYAYMYHOZXPYAYNOXSXPYAYMYHXPYAYMUMZBPQZYHXPYAYPYHOYMYPYEPUDZQZXPYHB
      PUNYRYEPQZXPYHYRYQPQYSUOYEYQPURUPYSYGPYFMZQZXPYHYEPYFUQUUAYGPQZXPYHUUAYTP
      QUUBYFUSYGYTPURUPUUBPYGQZXPYHYGPUTXPPCKZUUCYHCVAZPYGCVBVCVDRRRRSYOYPVJZYH
      YOUUFLYGECXPYAYMUUFYGEQZYAYMUUFUUGOOOXPYAYMUUFUUGYAYMUUFUMZYGEYAYMYGENUUF
      YAYEVEZYGEYEYFVFYAUUIBVGZEBVHZEXTBVKZVIVLSUUHEYEXTMZYGYAUUFEUUMQYMYAUUFLZ
      EUUJYEYEVGZMZUUMYAEUUJQUUFYAUUJEUULVMTUUJUUPQUUNUUJUUIUUPUUKYEVNVOVPUUNUU
      OXTYEUUNUUOBVEZXTBVQUUNEXTBVRZUUQXTQUUFYABPWBUURBPWCEABVSVTEXTBWAWMVIWDWE
      WFYMYAUUMYGNZUUFYMXTYFNUUSAYFUIXTYFYEWGWMWHWIWJWKVPWLYOECKZUUFXPYAUUTYMCE
      GWNSTWOWPWQWKWRWSYKYMVJZYHYKUVALYGPCYBUVAUUBXPYBYAXTYFXEZPQUUBUVAXSYAWTUV
      AUVBYFXTXEZPXTYFXAUVCPQUVAYFAXBXCXDEXTYFBXFXGXHXPUUDYBUVAUUEXIWOWPWQWRXJX
      KXLXRYCYJXOYBIBCDEFGHXMTXN $.
      $( [15-Jan-2007] $)
  $}

  ${
    $d o s t A $.  $d o s t F $.  $d o s t J $.  $d o s t K $.  $d o s t X $.
    cnsubsp.1 $e |- X = U. J $.
    $( Continuity of a restriction from a subspace.  (Contributed by Jeff
       Hankins, 11-Jul-2009.) $)
    cnsubsp $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( A C_ X /\ F e. ( J Cn K ) )
    ) -> ( F |` A ) e. ( ( subSp ` <. A , J >. ) Cn K ) ) $=
      ( vo ctop wcel wa wss ccn co cuni wf ccnv cvv wceq 3expa adantrl cres cop
      csubsp cfv cv cima wral wb topopn ssexg ancoms sylan stoig3 syldan simplr
      ad2ant2r eqid iscn syl2anc wfun cdm crn w3a cnf ffun funres 3syl sseqtr4d
      simprl fdm syl ssdmres sylib jca resss rnss ax-mp frn syl5ss df-fn anbi1i
      wfn df-f bitri sylanbrc simpll sseq2i biimpi stoig2 feq2d mpbid cnvresima
      ad2antrl cin simplll adantr cnima imp adantlrl elsubspr syl3anc ralrimiva
      wi ex syl5eqel mpbir2and ) CHIZDHIZJZAEKZBCDLMIZJZJZBAUAZACUBUCUDZDLMIZXO
      NZDNZXNOZXNPGUEZUFZXOIZGDUGZXMXOHIZXHXPXSYCJUHXGXJYDXHXKXGXJAQIZYDXGECIZX
      JYECEFUIXJYFYEAECUJUKULZACQUMUNUPXGXHXLUOGXNXODXQXRXQUQXRUQZURUSXMAXRXNOZ
      XSXMXNUTZXNVAARZJZXNVBZXRKZYIXMYJYKXIXKYJXJXGXHXKYJXGXHXKVCZEXRBOZBUTYJBC
      DEXRFYHVDZEXRBVEABVFVGSTXMABVAZKYKXMAEYRXIXJXKVIXIXKYRERZXJXIXKJZYPYSXGXH
      XKYPYQSEXRBVJVKTVHABVLVMVNXMYMBVBZXRXNBKYMUUAKBAVOXNBVPVQXIXKUUAXRKZXJXGX
      HXKUUBYOYPUUBYQEXRBVRVKSTVSYIXNAWBZYNJYLYNJAXRXNWCUUCYLYNXNAVTWAWDWEXMAXQ
      XRXNXMXGACNZKZAXQRXGXHXLWFXJUUEXIXKXJUUEEUUDAFWGWHWMACWIUSWJWKXMYBGDXMXTD
      IZJZYABPXTUFZAWNZXOAXTBWLUUGXGYEUUHCIZUUIXOIXGXHXLUUFWOXMYEUUFXGXJYEXHXKY
      GUPWPXIXKUUFUUJXJYTUUFUUJXGXHXKUUFUUJXCYOUUFUUJXTBCDWQXDSWRWSUUHACQWTXAXE
      XBXF $.
      $( [15-Dec-2013] $) $( [11-Jul-2009] $)
  $}

  ${
    $d o s t B $.  $d o s t F $.  $d o s t J $.  $d o s t K $.  $d o s t Y $.
    cnsubsp2.1 $e |- Y = U. K $.
    $( Equivalence of continuity in the parent topology and continuity in a
       subspace.  (Contributed by Jeff Hankins, 10-Jul-2009.) $)
    cnsubsp2 $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( ran F C_ B /\ B C_ Y ) )
    -> ( F e. ( J Cn K ) <-> F e. ( J Cn ( subSp ` <. B , K >. ) ) ) ) $=
      ( vo vs vt ctop wcel wa wss cima wi cin wceq wb cvv syl cuni wf ccnv wral
      crn cv cop csubsp cfv ccn co wfn ffn simpr anim1i df-f sylibr exp31 com23
      imp syl5 adantrr adantrd wfun ffun wrex sseq2i biimpi adantl uniexg ssexg
      adantr syl2anc elsubsp syldan ad2ant2l imaeq2 ad2antlr funcnvcnv ad2antrl
      imain eqtrd cdm cnvimarndm imass2 ad2antrr syl5eqssr cnvimass eqss ineq2d
      jctil df-ss mpbi syl6eq eleq1d rcla4v ad2ant2rl eqeltrd rexlimdv ralrimdv
      sylbid sylani jcad fss expcom ad2antll simplr simprr elsubspr syl3anc a1i
      exp41 3eqtrd sylibd ex expimpd impbid stoig2 sylan2b feq3 bitrd eqid iscn
      anbi1d stoig3 sylan2 anassrs adantrl 3bitr4d ) CJKZDJKZLZBUEZAMZAEMZLZLZC
      UAZEBUBZBUCZGUFZNZCKZGDUDZLZYRADUGUHUIZUAZBUBZYTHUFZNZCKZHUUFUDZLZBCDUJUK
      KZBCUUFUJUKKZYQUUEYRABUBZUULLZUUMYQUUEUUQYQUUEUUPUULYQYSUUPUUDYLYNYSUUPOY
      OYSBYRULZYLYNLUUPYREBUMYLYNUURUUPOYLUURYNUUPYLUURYNUUPYLUURLZYNLUURYNLUUP
      UUSUURYNYLUURUNUOYRABUPUQURUSUTVAVBVCYSYQBVDZUUDUULYREBVEYQUUTUUDLZUUKHUU
      FYQUUIUUFKZUVAUUKYQUVBUUIIUFZAPZQZIDVFZUVAUUKOZYKYOUVBUVFRZYJYNYKYOASKZUV
      HYKYOLZADUAZMZUVKSKZUVIYOUVLYKYOUVLEUVKAFVGZVHVIYKUVMYODJVJVLAUVKSVKVMZIU
      UIADSVNVOVPYQUVEUVGIDYQUVCDKZUVEUVAUUKYQUVPLZUVELZUVALZUUJYTUVCNZYTANZPZC
      UVSUUJYTUVDNZUWBUVEUUJUWCQUVQUVAUUIUVDYTVQVRUVSYTUCVDZUWCUWBQUUTUWDUVRUUD
      BVSZVTUVCAYTWATWBUVSUWBUVTCUVSUWBUVTBWCZPZUVTUVSUWAUWFUVTUVSUWAUWFMZUWFUW
      AMZLZUWAUWFQZUVSUWIUWHUVSUWFYTYMNZUWABWDZUVQUWLUWAMZUVEUVAYPUWNYLUVPYNUWN
      YOYMAYTWEZVLVRWFWGBAWHZWKUWAUWFWIZUQWJUVTUWFMUWGUVTQBUVCWHUVTUWFWLWMWNUVQ
      UUDUVTCKZUVEUUTUVQUUDUWRUVPUUDUWROYQUUCUWRGUVCDUUAUVCQUUBUVTCUUAUVCYTVQWO
      WPVIUTWQWRWRXLWSXAUSWTXBXCYQUUQYSUUDYQUUPYSUULYOUUPYSOYLYNUUPYOYSYRAEBXDX
      EXFVCUUPYQUUTUULUUDYRABVEYQUUTUULLUUCGDYQUUTUULUUADKZUUCOYQUUTLZUWSUULUUC
      UWTUWSUULUUCOUWTUWSLZUULYTUUAAPZNZCKZUUCUXAUXBUUFKZUULUXDOUXAYKUVIUWSUXEY
      QYKUUTUWSYJYKYPXGWFZUXAYKYOUVIUXFYQYOUUTUWSYLYNYOXHWFUVOVMUWTUWSUNUUAADSX
      IXJUUKUXDHUXBUUFUUIUXBQUUJUXCCUUIUXBYTVQWOWPTUXAUXCUUBCUXAUXCUUBUWAPZUUBU
      WFPZUUBUXAUWDUXCUXGQUUTUWDYQUWSUWEVRUUAAYTWATUXAUWAUWFUUBUXAUWJUWKUXAUWIU
      WHUXAUWFUWLUWAUWMYQUWNUUTUWSYNUWNYLYOUWOVTWFWGUWPWKUWQUQWJUXHUUBQZUXAUUBU
      WFMUXIBUUAWHUUBUWFWLWMXKXMWOXNXOUSXPWTXBXCXQYQUUPUUHUULYQAUUGQZUUPUUHRYKY
      OUXJYJYNYOYKUVLUXJUVNADXRXSVPAUUGYRBXTTYDYAYLUUNUUERYPGBCDYREYRYBZFYCVLYL
      YOUUOUUMRZYNYJYKYOUXLUVJYJUUFJKZUXLYKYOUVIUXMUVOADSYEVOHBCUUFYRUUGUXKUUGY
      BYCYFYGYHYI $.
      $( [15-Dec-2013] $) $( [10-Jul-2009] $)
  $}

  ${
    $d o s t B $.  $d o s t F $.  $d o s t J $.  $d o s t K $.  $d o s t Y $.
    cnsubsp2r.1 $e |- Y = U. K $.
    $( Equivalence of continuity in the parent topology and continuity in a
       subspace.  (Contributed by Mario Carneiro, 7-Jun-2014.) $)
    cnsubsp2r $p |- ( ( ( J e. Top /\ K e. Top ) /\
                      ( B C_ Y /\ F e. ( J Cn ( subSp ` <. B , K >. ) ) ) )
                      -> F e. ( J Cn K ) ) $=
      ( ctop wcel wa wss cop csubsp cfv ccn co simprr cuni cvv syl2anc eqid crn
      wb simpll simplr wf wceq simprl syl6sseq stoig2 uniex syl6eqel stoig3 cnf
      fvex syl3anc frn syl sseqtr4d cnsubsp2 syl22anc mpbird ) CGHZDGHZIZAEJZBC
      ADKZLMZNOHZIZIZBCDNOHZVHVDVEVHPZVJVBVCBUAZAJVEVKVHUBVBVCVIUCZVBVCVIUDZVJV
      MVGQZAVJCQZVPBUEZVMVPJVJVBVGGHZVHVRVNVJVCARHVSVOVJAVPRVJVCADQZJAVPUFVOVJA
      EVTVDVEVHUGZFUHADUISZVGVFLUNUJUKADRULSVLBCVGVQVPVQTVPTUMUOVQVPBUPUQWBURWA
      ABCDEFUSUTVA $.
      $( [7-Jun-2014] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z F $.  $d x y z J $.  $d x y z K $.
    $d x y z P $.  $d x y z X $.  $d x y Y $.
    cnpsubsp.1 $e |- X = U. J $.
    cnpsubsp.2 $e |- Y = U. K $.
    $( Equivalence of continuity at a point and continuity of the restricted
       function at a point.  (Contributed by Mario Carneiro, 8-Aug-2014.) $)
    cnpsubsp $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( A C_ X /\
                       P e. ( ( int ` J ) ` A ) /\ F : X --> Y ) ) ->
      ( F e. ( ( J CnP K ) ` P ) <->
        ( F |` A ) e. ( ( ( subSp ` <. A , J >. ) CnP K ) ` P ) ) ) $=
      ( vy vx vz wcel wa wss cfv cima wrex wceq syl2anc ctop cnt wf w3a cv wral
      wi cres cop csubsp co simpll simpr1 ntrss2 simpr2 sseldd fvres syl eqcomd
      ccnp eleq1d cin idd jctird elin syl6ibr inss1 inss2 ax-mp imass2 syl5eqss
      resima2 sstr2 mp2b anim12d reximdv ntropn inopn 3com23 3expia sseld sslin
      3syl syl6sseqr eleq2 imaeq2 sseq1d anbi12d rcla4ev syl6 expdimp rexlimdva
      a1i cbvrexv syl6ib impbid cab cuni syl6sseq uniexg ssexg subspval rexeqdv
      cvv wex eqeq1 rexbidv rexab rexcom4 vex inex1 ceqsexv rexbii exbii bitr4i
      r19.41v 3bitr3i syl6bb bitr4d imbi12d ralbidv simplr iscnp syl3anc simpr3
      wb biantrurd stoig3 stoig2 eleqtrd eqid fssres feq2d mpbid 3bitr4d ) DUAM
      ZEUAMZNZAFOZBADUBPPZMZFGCUCZUDZNZBCPZJUEZMZBKUEZMZCUUHQZUUFOZNZKDRZUGZJEU
      FZBCAUHZPZUUFMZBLUEZMZUUPUUSQZUUFOZNZLADUIUJPZRZUGZJEUFZCBDEUTUKPMZUUPBUV
      DEUTUKPMZUUDUUNUVFJEUUDUUGUURUUMUVEUUDUUEUUQUUFUUDUUQUUEUUDBAMZUUQUUESUUD
      YTABUUDYPYSYTAOZYPYQUUCULZYRYSUUAUUBUMZADFHUNTZYRYSUUAUUBUOZUPZBACUQURUSV
      AUUDUUMBUUHAVBZMZUUPUVQQZUUFOZNZKDRZUVEUUDUUMUWBUUDUULUWAKDUUDUUIUVRUUKUV
      TUUDUUIUUIUVJNUVRUUDUUIUUIUVJUUDUUIVCUVPVDBUUHAVEVFUUKUVTUGZUUDUVQUUHOZUV
      SUUJOUWCUUHAVGZUWDUVSCUVQQZUUJUVQAOUVSUWFSUUHAVHCUVQAVLVIZUVQUUHCVJVKUVSU
      UJUUFVMVNWMVOVPUUDUWBUUTCUUSQZUUFOZNZLDRZUUMUUDUWAUWKKDUUDUUHDMZUWAUWKUUD
      UWLUWANUUHYTVBZDMZBUWMMZCUWMQZUUFOZNZNUWKUUDUWLUWNUWAUWRUUDYPYTDMZUWLUWNU
      GUVLUUDYPYSUWSUVLUVMADFHVQTYPUWSUWLUWNYPUWLUWSUWNUUHYTDVRVSVTTUUDUVRUWOUV
      TUWQUUDUVRUUIUUANUWOUUDUVRUUIUUAUUDUVQUUHBUWDUUDUWEWMWAUVOVDBUUHYTVEVFUUD
      UWPUVSOUVTUWQUGUUDUWPUWFUVSUUDUVKUWMUVQOUWPUWFOUVNYTAUUHWBUWMUVQCVJWCUWGW
      DUWPUVSUUFVMURVOVOUWJUWRLUWMDUUSUWMSZUUTUWOUWIUWQUUSUWMBWEUWTUWHUWPUUFUUS
      UWMCWFWGWHWIWJWKWLUWJUULLKDUUSUUHSZUUTUUIUWIUUKUUSUUHBWEUXAUWHUUJUUFUUSUU
      HCWFWGWHWNWOWPUUDUVEUVCLUUFUVQSZKDRZJWQZRZUWBUUDUVCLUVDUXDUUDYPAXDMZUVDUX
      DSUVLUUDADWRZOZUXGXDMZUXFUUDAFUXGUVMHWSZUUDYPUXIUVLDUAWTURAUXGXDXATZJKADX
      DXBTXCUXEUUSUVQSZKDRZUVCNZLXEZUWBUXCUXMUVCJLUUFUUSSUXBUXLKDUUFUUSUVQXFXGX
      HUXLUVCNZLXEZKDRUXPKDRZLXEUWBUXOUXPKLDXIUXQUWAKDUVCUWALUVQUUHAKXJXKUXLUUT
      UVRUVBUVTUUSUVQBWEUXLUVAUVSUUFUUSUVQUUPWFWGWHXLXMUXRUXNLUXLUVCKDXPXNXQXOX
      RXSXTYAUUDUVHUUBUUONZUUOUUDYPYQBFMUVHUXSYFUVLYPYQUUCYBZUUDAFBUVMUVPUPKJBC
      DEFGHIYCYDUUDUUBUUOYRYSUUAUUBYEZYGXSUUDUVIUVDWRZGUUPUCZUVGNZUVGUUDUVDUAMZ
      YQBUYBMUVIUYDYFUUDYPUXFUYEUVLUXKADXDYHTUXTUUDBAUYBUVPUUDYPUXHAUYBSUVLUXJA
      DYITZYJLJBUUPUVDEUYBGUYBYKIYCYDUUDUYCUVGUUDAGUUPUCZUYCUUDUUBYSUYGUYAUVMFG
      ACYLTUUDAUYBGUUPUYFYMYNYGXSYO $.
      $( [8-Aug-2014] $)

    $( Equivalence of point-continuity in the parent topology and
       point-continuity in a subspace.  (Contributed by Mario Carneiro,
       9-Aug-2014.) $)
    cnpsubsp2 $p |- ( ( ( J e. Top /\ K e. Top ) /\
      ( P e. X /\ F : X --> B /\ B C_ Y ) ) -> ( F e. ( ( J CnP K ) ` P ) <->
        F e. ( ( J CnP ( subSp ` <. B , K >. ) ) ` P ) ) ) $=
      ( vx vy vz wcel wa wss wrex wi wral syl2anc cvv ctop wf w3a cfv cv csubsp
      cima cop ccnp cin simpr2 simpr1 ffvelrn biantrud elin syl6bbr crn imassrn
      frn syl syl5ss ssin syl6bb anbi2d rexbidv imbi12d ralbidv wceq cab simplr
      co simpr3 cuni uniexg syl5eqel ssexg subspval raleqdv eqeq1 ralab ralcom4
      wal vex inex1 eleq2 sseq2 ceqsalv ralbii r19.23v 3bitr3i bitr4i bitr4d wb
      albii simpll iscnp syl3anc fss biantrurd stoig3 eqid syl6sseq stoig2 feq3
      mpbid 3bitr4d ) DUAMZEUAMZNZBFMZFACUBZAGOZUCZNZBCUDZJUEZMZBKUEZMZCXRUGZXP
      OZNZKDPZQZJERZXOLUEZMZXSXTYFOZNZKDPZQZLAEUHUFUDZRZCBDEUIVKUDMZCBDYLUIVKUD
      MZXNYEXOXPAUJZMZXSXTYPOZNZKDPZQZJERZYMXNYDUUAJEXNXQYQYCYTXNXQXQXOAMZNYQXN
      UUCXQXNXKXJUUCXIXJXKXLUKZXIXJXKXLULZFABCUMSUNXOXPAUOUPXNYBYSKDXNYAYRXSXNY
      AYAXTAOZNYRXNUUFYAXNXTCUQZACXRURXNXKUUGAOUUDFACUSUTVAUNXTXPAVBVCVDVEVFVGX
      NYMYKLXRYPVHZJEPZKVIZRZUUBXNYKLYLUUJXNXHATMZYLUUJVHXGXHXMVJZXNXLGTMZUULXI
      XJXKXLVLZXNXHUUNUUMXHGEVMZTIEUAVNVOUTAGTVPSZKJAETVQSVRUUKYFYPVHZJEPZYKQZL
      WBZUUBUUIUUSYKLKXRYFVHUUHUURJEXRYFYPVSVEVTUURYKQZLWBZJERUVBJERZLWBUUBUVAU
      VBJLEWAUVCUUAJEYKUUALYPXPAJWCWDUURYGYQYJYTYFYPXOWEUURYIYSKDUURYHYRXSYFYPX
      TWFVDVEVFWGWHUVDUUTLUURYKJEWIWNWJWKVCWLXNYNFGCUBZYENZYEXNXGXHXJYNUVFWMXGX
      HXMWOZUUMUUEKJBCDEFGHIWPWQXNUVEYEXNXKXLUVEUUDUUOFAGCWRSWSWLXNYOFYLVMZCUBZ
      YMNZYMXNXGYLUAMZXJYOUVJWMUVGXNXHUULUVKUUMUUQAETWTSUUEKLBCDYLFUVHHUVHXAWPW
      QXNUVIYMXNXKUVIUUDXNAUVHVHZXKUVIWMXNXHAUUPOUVLUUMXNAGUUPUUOIXBAEXCSAUVHFC
      XDUTXEWSWLXF $.
      $( [9-Aug-2014] $)
  $}

  ${
    $d J y $.  $d K y $.  $d A y $.  $d B y $.  $d X y $.  $d Y y $.  $d F y $.
    paste.1 $e |- X = U. J $.
    paste.2 $e |- Y = U. K $.
    $( Pasting lemma.  If ` A ` and ` B ` are closed sets in ` X ` with
       ` A u. B = X ` , then any function whose restrictions to ` A ` and ` B `
       are continuous is continuous on all of ` X ` .  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    paste $p |- ( ( ( J e. Top /\ K e. Top ) /\
            ( A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) /\ ( A u. B ) = X ) /\
              ( F : X --> Y /\ ( F |` A ) e. ( ( subSp ` <. A , J >. ) Cn K )
                      /\ ( F |` B ) e. ( ( subSp ` <. B , J >. ) Cn K ) ) )
                                          -> F e. ( J Cn K ) ) $=
      ( vy ctop wcel wa ccld cfv wceq w3a cin adantr wi cun cres cop csubsp ccn
      wf co ccnv cv cima wral wb iscncl 3ad2ant1 simp1 3ad2ant3 wss crn imassrn
      cdm dfdm4 fdm syl5eqr syl5sseq df-ss biimpi eqcomd syl adantl eqcoms wfun
      ineq2 ffun respreima uneq12d syl6reqr 3ad2antr1 3ad2antl3 3adant1 simpl1l
      indi 3eqtrd stoig3 cnclima 3expia sylan an32s subspcld2 adantlr 3ad2antr2
      ex imp syld 3impia 3ad2antr3 uncld syl3anc eqeltrd ralrimiva mpbir2and )
      DKLZEKLZMZADNOZLZBXDLZABUAZFPZQZFGCUFZCAUBZADUCUDOZEUEUGLZCBUBZBDUCUDOZEU
      EUGLZQZQZCDEUEUGLZXJCUHZJUIZUJZXDLZJENOZUKZXCXIXSXJYEMULXQJCDEFGHIUMUNXQX
      CXJXIXJXMXPUOUPXRYCJYDXRYAYDLZMZYBXKUHYAUJZXNUHYAUJZUAZXDXRYBYJPZYFXIXQYK
      XCXHXEXQYKXFXHXMXJYKXPXHXJMYBYBFRZYBXGRZYJXJYBYLPZXHXJYBFUQZYNXJXTURZYBFX
      TYAUSXJYPCUTFCVAFGCVBVCVDYOYLYBYOYLYBPYBFVEVFVGVHVIXHYLYMPZXJYQFXGFXGYBVL
      VJSXJYMYJPXHXJYJYBARZYBBRZUAZYMXJCVKZYJYTPFGCVMUUAYHYRYIYSYAACVNYABCVNVOV
      HYBABWAVPVIWBVQVRVSSYGXAYHXDLZYIXDLZYJXDLXAXBXIXQYFVTXRYFUUBXCXIXQYFUUBTZ
      XCXFXEXQUUDTXHXCXEMZXQUUDUUEXJXMUUDXPUUEXMMYFYHXLNOLZUUBUUEXMYFUUFTZXAXEX
      BXMUUGTZXAXEMXLKLZXBUUHADXDWCUUIXBXMUUGUUIXBXMQYFUUFYAXKXLEWDWKWEWFWGWLUU
      EUUFUUBTZXMXAXEUUJXBXAXEUUFUUBAYHDWHWEWISWMWJWKVQWNWLXRYFUUCXCXIXQYFUUCTZ
      XCXEXFXQUUKTXHXCXFMZXQUUKUULXJXPUUKXMUULXPMYFYIXONOLZUUCUULXPYFUUMTZXAXFX
      BXPUUNTZXAXFMXOKLZXBUUOBDXDWCUUPXBXPUUNUUPXBXPQYFUUMYAXNXOEWDWKWEWFWGWLUU
      LUUMUUCTZXPXAXFUUQXBXAXFUUMUUCBYIDWHWEWISWMWOWKWJWNWLYHYIDWPWQWRWSWT $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

