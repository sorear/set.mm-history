$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Connectedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Con $.
  $( Extend class notation with the the class of all connected topologies. $)
  ccon $a class Con $.

  $( Topologies are connected when only ` (/) ` and ` U. j ` are both open and
     closed. $)
  df-con $a |- Con = { j e. Top | ( j i^i ( Clsd ` j ) ) = { (/) , U. j } } $.

  ${
    $d J j $.
    $( The predicate ` J ` is a connected topology .  (Contributed by FL,
       17-Nov-2008.) $)
    iscon $p |- ( J e. Top -> ( J e. Con <-> ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj ccon wcel ctop ccld cfv cin c0 cuni cpr wceq cv fveq2 ineq12d preq2d
      id unieq eqeq12d df-con elrab2 baib ) ACDAEDAAFGZHZIAJZKZLZBMZUHFGZHZIUHJ
      ZKZLUGBAECUHALZUJUDULUFUMUHAUIUCUMQUHAFNOUMUKUEIUHARPSBTUAUB $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)

    $( The predicate ` J ` is a connected topology .  (Contributed by FL,
       17-Nov-2008.) $)
    iscon2 $p |- ( J e. Con <-> ( J e. Top /\ ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj cv ccld cfv cin c0 cuni wceq ctop ccon id fveq2 ineq12d unieq preq2d
      cpr eqeq12d df-con elrab2 ) BCZUADEZFZGUAHZQZIAADEZFZGAHZQZIBAJKUAAIZUCUG
      UEUIUJUAAUBUFUJLUAADMNUJUDUHGUAAOPRBST $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)
  $}

  ${
    $d J x y $.  $d X y $.
    usinuniop.1 $e |- X = U. J $.
    $( If a topology is connected, its underlying set can't be partitioned into
       two non empty non-overlapping open sets.  (Contributed by FL,
       17-Nov-2008.) $)
    usinuniop $p |- ( J e. Con -> A. x e. J A. y e. J
       ( ( x =/= (/) /\ y =/= (/) /\ ( x i^i y ) = (/) )
         -> X =/= ( x u. y ) ) ) $=
      ( wcel c0 wne wceq wi wa wn wo wal wb impcom sylibr sylbir ex sylbi eleq1
      ccon cv cin w3a cun cdif ctop ccld cfv cuni cpr iscon2 opncld elin biimpd
      eleq2 vex elpr olc a1i12 com13 eqcomi eqeq2i eqeq2 wss eltopss cab df-dif
      dfss2 eqtr abeq2 pm4.71 albii 19.26 imim1 pm2.01 alimi eq0 syl eqcoms mpi
      syl6 com12 imp orcd syl6bi jaoi syl6com com24 pm2.43a com3l mpancom com14
      expcom imp41 nne orbi12i ianor con2d sylanb 3adant3 elssuni sseq2i syl5bb
      eqcom uneqdifeq 3ad2ant3 syl5com ad2antrl necon3abid mpbird ralrimivv
      exp31 ) CUBFZAUCZGHZBUCZGHZXPXRUDGIZUEZDXPXRUFZHZJABCCXOXPCFZXRCFZKZYAYCX
      OYFKZYAKZYCDXPUGZXRIZLZYAYGYKXQXSYGYKJXTYGXQXSKZYKXOCUHFZCCUIUJZUDZGCUKZU
      LZIZKZYFYLYKJCUMYSYFKZYJYLYTYJYLLZYTYJKZXQLZXSLZMZUUAUUBXPGIZXRGIZMZUUEYM
      YRYFYJUUHYFYRYMYJUUHJZYEYDYRYMUUIJJYMYDYRYEUUIYMYDYRYEUUIJJZYIYNFZYMYDKZU
      UJXPCDEUNUUKUULKYJYEYRUUHUUKUULYJYEYRUUHJZJZJYJUUKUULUUNUUKYJUULUUNJZYJUU
      KXRYNFZYJUUOJYIXRYNUAUUPYEUULYJUUMYEUUPUULYJUUMJJZYEUUPKXRYOFZUUQXRCYNUOU
      URYRYJUULUUHYRUURXRYQFZYJUULUUHJZJZYRUURUUSYOYQXRUQUPUUSUUGXRYPIZMUVAXRGY
      PBURUSUUGUVAUVBUULYJUUGUUHUULYJUUGUUHJUUGUUFUTVAVBUVBXRDIZUVAYPDXRDYPEVCZ
      VDUVCYJYIDIZUUTXRDYIVEUULUVEUUHUULXPDVFZUVEUUHJXPCDEVGUVFUVEUUHUVFUVEKUUF
      UUGUVFUVEUUFUVFXRXPFZXRDFZJZBNZUVEUUFJBXPDVJUVEUVJUUFUVEYIUVHUVGLZKZBVHZI
      ZUVJUUFJZBDXPVIUVNUVOJDYIDYIIZUVNUVOUVPUVNKDUVMIZUVODYIUVMVKUVQUVHUVLOZBN
      ZUVOUVLBDVLUVSUVHUVKJZBNZUVOUVTUVRBUVHUVKVMVNUWAUVJUUFUWAUVJKUVTUVIKZBNZU
      UFUVTUVIBVOUWCUVKBNUUFUWBUVKBUVIUVTUVKUVIUVTUVGUVKJUVKUVGUVHUVKVPUVGVQWCP
      VRBXPVSQRSRTVTSWAWBWDTWEWFSVTWDWGTWHTWIWJRWOWJWGWKWLWEWJWMSWNPVBWPUUCUUFU
      UDUUGXPGWQXRGWQWRQXQXSWSQSWTXAWDXBPYHYJDYBYGYADYBIZYJOZYDYAUWEJXOYEYDXPYP
      VFZYAUWEXPCXCXTXQUWFUWEJXSUWFXTUWEUWDYBDIZUWFXTKYJDYBXFUWFUVFXTUWGYJOYPDX
      PUVDXDXPXRDXGXAXEWOXHXIXJWEXKXLXNXM $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)
  $}

  ${
    $d o x y J $.  $d o x y X $.
    dfcon2.1 $e |- X = U. J $.
    $( An alternate definition of connectedness.  (Contributed by Jeff Hankins,
       9-Jul-2009.) $)
    dfcon2 $p |- ( J e. Top -> ( J e. Con <-> A. x e. J A. y e. J ( ( x =/= (/)
    /\ y =/= (/) /\ ( x i^i y ) = (/) ) -> X =/= ( x u. y ) ) ) ) $=
      ( vo wcel cv c0 wne cin wceq wi wa wn adantl adantr wb eleq1 mpbird neeq1
      ctop ccon w3a cun wral usinuniop ccld cfv cuni cpr wo cdif simprl adantrl
      cldopn ineq1 eqeq1d 3anbi13d uneq1 imbi12d ineq2 3anbi23d rcla42v syl2anc
      neeq2d uneq2 disjdif jctr df-3an sylibr imim1i necon2bd wss eltopss undif
      id biimpi eqcomd pm2.27 anbi12i notbii oran bitr4i simplbi2 ssdif0 eqeq2i
      df-ne eqss 3imtr3g orim2d com12 sylbi syl6 com3r syldan adantrr syl5 syld
      mpd impancom 0opn 0cld jca ex eqid topopn topcld jaod impbid elin 3bitr4g
      vex elpr eqrdv iscon sylibrd impbid2 ) CUBGZCUCGZAHZIJZBHZIJZYAYCKZILZUDZ
      DYAYCUEZJZMZBCUFACUFZABCDEUGXSYKCCUHUIZKZICUJZUKZLZXTXSYKYPXSYKNZFYMYOYQF
      HZCGZYRYLGZNZYRILZYRYNLZULZYRYMGYRYOGYQUUAUUDXSUUAYKUUDXSUUANZYKYRIJZDYRU
      MZIJZYRUUGKZILZUDZDYRUUGUEZJZMZUUDUUEYSUUGCGZYKUUNMXSYSYTUNXSYTUUOYSYRCDE
      UPUOYJUUNUUFYDYRYCKZILZUDZDYRYCUEZJZMABYRUUGCCYAYRLZYGUURYIUUTUVAYBUUFYFU
      UQYDYAYRIUAUVAYEUUPIYAYRYCUQURUSUVAYHUUSDYAYRYCUTVFVAYCUUGLZUURUUKUUTUUMU
      VBYDUUHUUQUUJUUFYCUUGIUAUVBUUPUUIIYCUUGYRVBURVCUVBUUSUULDYCUUGYRVGVFVAVDV
      EUUNUUFUUHNZUUMMZUUEUUDUVCUUKUUMUVCUVCUUJNUUKUVCUUJYRDVHVIUUFUUHUUJVJVKVL
      UVDDUULLZUVCOZMZUUEUUDUVDUVCDUULUVDVQVMXSYSUVGUUDMZYTXSYSYRDVNZUVHYRCDEVO
      XSUVINZUVEUVHUVIUVEXSUVIUULDUVIUULDLYRDVPVRVSPUVEUVGUVJUUDUVEUVGUVFUVJUUD
      MZUVEUVFVTUVFUUBUUGILZULZUVKUVFUUBOZUVLOZNZOUVMUVCUVPUUFUVNUUHUVOYRIWHUUG
      IWHWAWBUUBUVLWCWDUVJUVMUUDUVJUVLUUCUUBUVJDYRVNZYRDLZUVLUUCUVIUVQUVRMXSUVR
      UVIUVQYRDWIWEPDYRWFDYNYREWGWJWKWLWMWNWOWTWPWQWRWRWSXAXSUUDUUAMYKXSUUBUUAU
      UCXSUUBUUAXSUUBNZYSYTUVSYSICGZXSUVTUUBCXBQUUBYSUVTRXSYRICSPTUVSYTIYLGZXSU
      WAUUBCXCQUUBYTUWARXSYRIYLSPTXDXEXSUUCUUAXSUUCNZYSYTUWBYSYNCGZXSUWCUUCCYNY
      NXFZXGQUUCYSUWCRXSYRYNCSPTUWBYTYNYLGZXSUWEUUCCYNUWDXHQUUCYTUWERXSYRYNYLSP
      TXDXEXIQXJYRCYLXKYRIYNFXMXNXLXOXECXPXQXR $.
      $( [9-Jul-2009] $)
  $}

  ${
    $d r s u v x y J $.  $d r s u v x y S $.  $d r s u v x y X $.
    connsub.1 $e |- X = U. J $.
    $( Two equivalent ways of saying that a subspace topology is connected.
       (Contributed by Jeff Hankins, 9-Jul-2009.) $)
    connsub $p |- ( ( J e. Top /\ S C_ X ) -> ( ( subSp ` <. S , J >. ) e. Con
    <-> A. x e. J A. y e. J ( ( ( x i^i S ) =/= (/) /\ ( y i^i S ) =/= (/) /\ (
    x i^i y ) C_ ( X \ S ) ) -> -. S C_ ( x u. y ) ) ) ) $=
      ( vu vv vr vs wcel wss wa c0 wne cin wceq w3a wi cvv ctop cop csubsp ccon
      cfv cv cuni cun wral cdif wn wb topopn ssexg syl2anr stoig3 syldan dfcon2
      id eqid syl sseq2i stoig2 neeq1d imbi2d 2ralbidv simp1l 3ad2ant1 elsubspr
      sylan2b simp2l syl3anc simp2r neeq1 ineq1 eqeq1d 3anbi13d neeq2d 3anbi23d
      uneq1 imbi12d ineq2 uneq2 rcla42v syl2anc inindir difss sstr reldisj ibir
      mpan2 syl5eqr 3anim3i 3ad2ant3 incom df-ss biimpi syl5reqr indir necon3ai
      syl6eq a1i embantd syld 3exp com34 com23 ralrimdvv uniexg elsubsp anbi12d
      reeanv sseq1d sseq2d notbid pm13.18 3ad2antr1 adantlr 3ad2antl3 3ad2antr2
      3ad2ant2 adantll ineq12 biimpa 3ad2antr3 eltopss ssinss1 ad2ant2r 3adant3
      wrex syl5eq adantr mpbid inss1 sseq1 mpbiri anim12i unss12 syl5ibrcom
      3jca a1d syl3an3 imp necon3bd ex rexlimdvv syl5bir sylbid impbid 3bitr2d
      ) DUAKZCELZMZCDUBUCUEZUDKZGUFZNOZHUFZNOZUUPUURPZNQZRZUUNUGZUUPUURUHZOZSZH
      UUNUIGUUNUIZUVBCUVDOZSZHUUNUIGUUNUIZAUFZCPZNOZBUFZCPZNOZUVKUVNPZECUJZLZRZ
      CUVKUVNUHZLZUKZSZBDUIADUIZUUMUUNUAKZUUOUVGULUUKUULCTKZUWFUULUULEDKUWGUUKU
      ULUSDEFUMCEDUNUOZCDTUPUQGHUUNUVCUVCUTURVAUUMUVIUVFGHUUNUUNUUMUVHUVEUVBUUM
      CUVCUVDUULUUKCDUGZLZCUVCQEUWICFVBZCDVCVJVDVEVFUUMUVJUWEUUMUVJUWDABDDUUMUV
      KDKZUVNDKZMZUVJUWDUUMUWNUVTUVJUWCUUMUWNUVTUVJUWCSUUMUWNUVTRZUVJUVMUVPUVLU
      VOPZNQZRZCUVLUVOUHZOZSZUWCUWOUVLUUNKZUVOUUNKZUVJUXASUWOUUKUWGUWLUXBUUKUUL
      UWNUVTVGZUUMUWNUWGUVTUWHVHZUUMUWLUWMUVTVKUVKCDTVIVLUWOUUKUWGUWMUXCUXDUXEU
      UMUWLUWMUVTVMUVNCDTVIVLUVIUXAUVMUUSUVLUURPZNQZRZCUVLUURUHZOZSGHUVLUVOUUNU
      UNUUPUVLQZUVBUXHUVHUXJUXKUUQUVMUVAUXGUUSUUPUVLNVNUXKUUTUXFNUUPUVLUURVOVPV
      QUXKUVDUXICUUPUVLUURVTVRWAUURUVOQZUXHUWRUXJUWTUXLUUSUVPUXGUWQUVMUURUVONVN
      UXLUXFUWPNUURUVOUVLWBVPVSUXLUXIUWSCUURUVOUVLWCVRWAWDWEUWOUWRUWTUWCUVTUUMU
      WRUWNUVSUWQUVMUVPUVSUWPUVQCPZNUVKUVNCWFUVSUXMNQZUVSUVQELZUXNUVSULUVSUVREL
      UXOECWGUVQUVREWHWKUVQCEWIVAWJWLWMWNUWTUWCSUWOUWBCUWSUWBCUWACPZUWSUWBUXPCU
      WAPZCCUWAWOUWBUXQCQCUWAWPWQWRUVKUVNCWSXAWTXBXCXDXEXFXGXHUUMUWEUVIGHUUNUUN
      UUMUUPUUNKZUURUUNKZMZUWEUVIUUMUXTUUPIUFZCPZQZIDYJZUURJUFZCPZQZJDYJZMZUWEU
      VISZUUKUULUWGUXTUYIULUULUWJUWITKUWGUUKUULUWJUWKWQDUAXICUWITUNUOUUKUWGMUXR
      UYDUXSUYHIUUPCDTXJJUURCDTXJXKUQUYIUYCUYGMZJDYJIDYJUUMUYJUYCUYGIJDDXLUUMUY
      KUYJIJDDUUMUYADKZUYEDKZMZUYKUYJUUMUYNUYKRZUWEUYBNOZUYFNOZUYAUYEPZUVRLZRZC
      UYAUYEUHZLZUKZSZUVIUYNUUMUWEVUDSUYKUWDVUDUYPUVPUYAUVNPZUVRLZRZCUYAUVNUHZL
      ZUKZSABUYAUYEDDUVKUYAQZUVTVUGUWCVUJVUKUVMUYPUVSVUFUVPVUKUVLUYBNUVKUYACVOV
      DVUKUVQVUEUVRUVKUYAUVNVOXMVQVUKUWBVUIVUKUWAVUHCUVKUYAUVNVTXNXOWAUVNUYEQZV
      UGUYTVUJVUCVULUVPUYQVUFUYSUYPVULUVOUYFNUVNUYECVOVDVULVUEUYRUVRUVNUYEUYAWB
      XMVSVULVUIVUBVULVUHVUACUVNUYEUYAWCXNXOWAWDYAUYOUVBVUDUVHUYOUVBVUDUVHSUYOU
      VBMZUYTVUCUVHVUMUYPUYQUYSUYKUUMUVBUYPUYNUYCUVBUYPUYGUYCUUSUUQUYPUVAUUPUYB
      NXPXQXRXSUYKUUMUVBUYQUYNUYGUVBUYQUYCUYGUUQUUSUYQUVAUURUYFNXPXTYBXSVUMUYRC
      PZNQZUYSVUMVUNUYBUYFPZNUYAUYECWFUYOUUQUVAVUPNQZUUSUYKUUMUVAVUQUYNUYKUVAVU
      QUYKUUTVUPNUUPUYBUURUYFYCVPYDXSYEYKVUMUYRELZVUOUYSULUYOVURUVBUUMUYNVURUYK
      UUKUYLVURUULUYMUUKUYLMUYAELVURUYADEFYFUYAUYEEYGVAYHYIYLUYRCEWIVAYMYTVUMVU
      BCUVDUYOUVBCUVDQZVUBSZUYKUUMUYNUUPUYALZUURUYELZMZUVBVUTSZUYCVVAUYGVVBUYCV
      VAUYBUYALUYACYNUUPUYBUYAYOYPUYGVVBUYFUYELUYECYNUURUYFUYEYOYPYQVVCUUMVVDUY
      NVVCVUTUVBVVCVUBVUSUVDVUALUUPUYAUURUYEYRCUVDVUAYOYSUUAWNUUBUUCUUDXCUUEXGX
      DXEUUFUUGUUHXGXHUUIUUJ $.
      $( [15-Dec-2013] $) $( [9-Jul-2009] $)
  $}

  $( A connected topology is a topology.  (Contributed by FL, 22-Dec-2008.) $)
  contop $p |- ( J e. Con -> J e. Top ) $=
    ( ccon wcel ctop ccld cfv cin c0 cuni cpr wceq iscon2 simplbi ) ABCADCAAEFG
    HAIJKALM $.
    $( [14-Dec-2013] $) $( [22-Dec-2008] $)


