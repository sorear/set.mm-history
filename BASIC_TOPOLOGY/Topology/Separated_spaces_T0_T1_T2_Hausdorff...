$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Separated spaces: T0, T1, T2 (Hausdorff) ...
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Fre $. $( The class of all the T1-spaces
    (also called Frechet spaces). $)

  $( Declare new symbol for Hausdorff space. $)
  $c Haus $.

  $( Extend class notation to include T1-spaces. $)
  ct1 $a class Fre $.

  $( Extend class notation with the class of all Hausdorff spaces. $)
  cha $a class Haus $.

  ${
    $d x a $.
    $( The class of all T1-spaces also called Frechet spaces.  Morris, Topology
       without tears, p. 30 ex. 3.  (Contributed by FL, 18-Jun-2007.) $)
    df-t1 $a |- Fre = { x e. Top | A. a e. U. x { a } e. ( Clsd ` x ) } $.
  $}

  ${
    $d j m n x y w z $.
    $( Define the class of all Hausdorff spaces.  A Hausdorff space is a
       topology in which distinct points have disjoint open neighborhoods.
       Definition of Hausdorff space in [Munkres] p. 98. $)
    df-haus $a |- Haus = { j e. Top | A. x e. U. j A. y e. U. j ( x =/= y ->
           E. n e. j E. m e. j ( x e. n /\ y e. m /\ ( n i^i m ) = (/) ) ) } $.
  $}

  ${
    $d J a x $.  $d X x $.
    ist1.1 $e |- X = U. J $.
    $( The predicate ` J ` is T1.  (Contributed by FL, 18-Jun-2007.) $)
    ist1 $p |- ( J e. Fre <->
      ( J e. Top /\ A. a e. X { a } e. ( Clsd ` J ) ) ) $=
      ( vx csn ccld cfv wcel cuni wral ctop ct1 wceq unieq syl6eqr eleq2d fveq2
      cv imbi12d ralbidv2 df-t1 elrab2 ) CSZFZESZGHZIZCUFJZKUEAGHZIZCBKEALMUFAN
      ZUHUKCUIBULUDUIIUDBIUHUKULUIBUDULUIAJBUFAODPQULUGUJUEUFAGRQTUAECUBUC $.
      $( [1-Dec-2010] $) $( [7-Jun-2007] $)
  $}

  ${
    $d j m n u v x y J $.  $d j x y X $.  $d m n x y P $.  $d m n y Q $.
    ishaus.1 $e |- X = U. J $.
    $( Express the predicate " ` J ` is a Hausdorff space." $)
    ishaus $p |- ( J e. Haus <-> ( J e. Top /\ A. x e. X A. y e. X ( x =/= y ->
               E. n e. J E. m e. J ( x e. n /\ y e. m /\
                   ( n i^i m ) = (/) ) ) ) ) $=
      ( vj cv wne wcel cin c0 wceq w3a wrex wi cuni wral raleqbidv ctop syl6eqr
      cha unieq rexeq rexeqbi1dv imbi2d df-haus elrab2 ) AIZBIZJZUJDIZKUKCIZKUM
      UNLMNOZCHIZPZDUPPZQZBUPRZSZAUTSULUOCEPZDEPZQZBFSZAFSHEUAUCUPENZVAVEAUTFVF
      UTERFUPEUDGUBZVFUSVDBUTFVGVFURVCULUQVBDUPEUOCUPEUEUFUGTTABHCDUHUI $.
      $( [8-Mar-2007] $)

    $( Neighborhood property of a Hausdorff space. $)
    hausnei $p |- ( ( J e. Haus /\ ( P e. X /\ Q e. X /\ P =/= Q ) ) ->
           E. n e. J E. m e. J ( P e. n /\ Q e. m /\
                   ( n i^i m ) = (/) ) ) $=
      ( vx vy wcel wne cv wceq w3a wrex wi wral eleq1 2rexbidv imbi12d cha ctop
      c0 wa ishaus simprbi neeq1 3anbi1d neeq2 3anbi2d rcla42v syl5 com3r 3imp2
      cin ex ) EUAJZAFJZBFJZABKZADLZJZBCLZJZVAVCUOUCMZNZCEODEOZURUSUQUTVGPZURUS
      UQVHPUQHLZILZKZVIVAJZVJVCJZVENZCEODEOZPZIFQHFQZURUSUDVHUQEUBJVQHICDEFGUEU
      FVPVHAVJKZVBVMVENZCEODEOZPHIABFFVIAMZVKVRVOVTVIAVJUGWAVNVSDCEEWAVLVBVMVEV
      IAVARUHSTVJBMZVRUTVTVGVJBAUIWBVSVFDCEEWBVMVDVBVEVJBVCRUJSTUKULUPUMUN $.
      $( [8-Mar-2007] $)
    $( The Hausdorff condition still holds if one considers general
       neighborhoods instead of open sets.  (Contributed by Jeff Hankins,
       5-Sep-2009.) $)
    hausnei2 $p |- ( J e. Top -> ( J e. Haus <->
                     A. x e. X A. y e. X ( x =/= y ->
               E. u e. ( ( nei ` J ) ` { x } ) E. v e. ( ( nei ` J ) ` { y } )
                  ( u i^i v ) = (/) ) ) ) $=
      ( vm vn wcel cv c0 wceq w3a wrex wi wral wa ex wss cha wne cin csn ishaus
      ctop cnei cfv baib simp1 adantr simp2 adantl opnneip syl3anc simp3 eqeq1d
      ineq1 ineq2 rcla42ev 3expib rexlimdvv neii2 vex snss anbi1i simp1l simp2l
      ss2in ssn0 necon4d syl ad2ant2l 3impia 3jca syl5bir com3r 3adant1 reximdv
      3exp imp com34 3imp com24 imp3a syl2and impbid imbi2d 2ralbidv bitrd ) EU
      FJZEUAJZAKZBKZUBZWMHKZJZWNIKZJZWPWRUCZLMZNZIEOZHEOZPZBFQAFQZWODKZCKZUCZLM
      ZCWNUDZEUGUHZUHZODWMUDZXLUHZOZPZBFQAFQWLWKXFABIHEFGUEUIWKXEXQABFFWKXDXPWO
      WKXDXPWKXBXPHIEEWKWPEJZWREJZXBXPPWKXRXSNZXBXPXTXBRZWPXOJZWRXMJZXAXPYAWKXR
      WQYBXTWKXBWKXRXSUJUKZXTXRXBWKXRXSULUKXBWQXTWQWSXAUJUMWMEWPUNUOYAWKXSWSYCY
      DXTXSXBWKXRXSUPUKXBWSXTWQWSXAULUMWNEWRUNUOXBXAXTWQWSXAUPUMXJXAWPXHUCZLMDC
      WPWRXOXMXGWPMXIYELXGWPXHURUQXHWRMYEWTLXHWRWPUSUQUTUOSVAVBWKXJXDDCXOXMWKXG
      XOJZXNWPTZWPXGTZRZHEOZXHXMJZXKWRTZWRXHTZRZIEOZXJXDPZWKYFYJXNHEXGVCSWKYKYO
      XKIEXHVCSWKYJYOYPWKXJYOYJXDWKXJYOYJXDPWKXJYONZYIXCHEYIWQYHRZYQXCWQYGYHWMW
      PAVDVEVFWKXJYOYRXCPWKXJYRYOXCWKXJYRYOXCPWKXJYRNYNXBIEXJYRYNXBPZWKXJYRYSYR
      YNXJXBYNWSYMRZYRXJXBPWSYLYMWNWRBVDVEVFYRYTXJXBYRYTXJNWQWSXAWQYHYTXJVGYRWS
      YMXJVHYRYTXJXAYHYMXJXAPZWQWSYHYMRWTXITZUUAWPXGWRXHVIUUBWTLXILUUBWTLUBXILU
      BWTXIVJSVKVLVMVNVOVTVPVQWAVRVSVTWBWCVPVSVTWDWEWFVBWGWHWIWJ $.
      $( [1-Dec-2010] $) $( [5-Sep-2009] $)
  $}

  ${
    $d m n x y J $.  $d x y X $.
    ishausi.1 $e |- X = U. J $.
    ishausi.2 $e |- J e. Top $.
    ishausi.3 $e |- ( ( x e. X /\ y e. X /\ x =/= y ) ->
             E. n e. J E. m e. J ( x e. n /\ y e. m /\ ( n i^i m ) = (/) ) ) $.
    $( Properties that determine a Hausdorff space. $)
    ishausi $p |- J e. Haus $=
      ( cha wcel ctop cv wne cin c0 wceq w3a wrex wral wi ishaus 3expia rgen2a
      mpbir2an ) EJKELKAMZBMZNZUFDMZKUGCMZKUIUJOPQRCESDESZUAZBFTAFTABCDEFGUBHUL
      ABFUFFKUGFKUHUKIUCUDUE $.
      $( [8-Mar-2007] $)
  $}

  ${
    $d x y m n J $.
    $( A Hausdorff space is a topology. $)
    haustop $p |- ( J e. Haus -> J e. Top ) $=
      ( vx vy vn vm cha wcel ctop cv wne cin c0 wceq w3a wrex wi cuni wral eqid
      ishaus simplbi ) AFGAHGBIZCIZJUBDIZGUCEIZGUDUEKLMNEAODAOPCAQZRBUFRBCEDAUF
      UFSTUA $.
      $( [5-Mar-2007] $)
  $}

  ${
    $d n x y J $.  $d n x y P $.  $d x y X $.
    sncld.1 $e |- X = U. J $.
    $( A singleton is closed in a Hausdorff space. $)
    sncld $p |- ( ( J e. Haus /\ P e. X ) -> { P } e. ( Clsd ` J ) ) $=
      ( vx vy vn wcel wa cfv wceq cv cin c0 wne wi wral wss wb wrex cha csn ccl
      ccld ctop haustop snssi elcls2 syl2an wn df-ne imbi2i ralbii ralinexa w3a
      bitri hausnei sslin sseq0 sylan anim2i 3impb rexlimivw reximi 3exp2 imp31
      syl necon1bd syl5bi expimpd eleq1a adantl biimpd disjsn necon2abii syl6ib
      an32s eleq1 ralrimivw jcad impbid elsn syl6bbr bitrd eqrdv iscld3 mpbird
      a1i ) BUAHZACHZIZAUBZBUDJHZWLBUCJJZWLKZWKEWNWLWKELZWNHZWPCHZWPFLZHZWSWLMZ
      NOZPZFBQZIZWPWLHZWIBUEHZWLCRZWQXESWJBUFZACUGZFWPWLBCDUHUIWKXEWPAKZXFWKXEX
      KWKWRXDXKXDWTXANKZIZFBTZUJZWKWRIZXKXDWTXLUJZPZFBQXOXCXRFBXBXQWTXANUKULUMW
      TXLFBUNUPXPXNWPAWIWRWJWPAOZXNPZWIWRWJXTWIWRWJXSXNWIWRWJXSUOIWTAGLZHZWSYAM
      ZNKZUOZGBTZFBTXNWPAGFBCDUQYFXMFBYEXMGBWTYBYDXMYBYDIXLWTYBXAYCRZYDXLYBWLYA
      RYGAYAUGWLYAWSURVGXAYCUSUTVAVBVCVDVGVEVFVQVHVIVJWKXKWRXDWJXKWRPWIACWPVKVL
      XKXDPWKXKXCFBXKWTAWSHZXBXKWTYHWPAWSVRVMYHXANWSAVNVOVPVSWHVTWAEAWBWCWDWEWI
      XGXHWMWOSWJXIXJWLBCDWFUIWG $.
      $( [5-Mar-2007] $)
  $}

  ${
    dnsconst.1 $e |- X = U. J $.
    dnsconst.2 $e |- Y = U. K $.
    $( If a continuous mapping to a Hausdorff space is constant on a dense
       subset, it is constant on the entire space.  Note that
       ` ( ( cls `` J ) `` A ) = X ` means " ` A ` is dense in ` X ` " and
       ` A C_ ( ``' F " { P } ) ` means " ` F ` is constant on ` A ` " (see
       ~ funconstss ). $)
    dnsconst $p |- ( ( ( J e. Top /\ K e. Haus /\ F e. ( J Cn K ) ) /\
  ( P e. Y /\ A C_ ( `' F " { P } ) /\ ( ( cls ` J ) ` A ) = X ) ) ->
        F : X --> { P } ) $=
      ( ctop wcel w3a wss cfv wceq wa wf ccld wi adantlr cha ccn co csn ccl wfn
      ccnv cima haustop cnf syl3an2 ffn adantr sncld 3ad2antl2 cnclima syl3anl2
      syldan 3ad2antr1 simpll cldss simplr clsss syl3anc cldcls sseqtrd adantrr
      syl ex wb sseq1 ad2antll sylibd 3adantr1 3adantl3 mpd fconst3 sylanbrc )
      DJKZEUAKZCDEUBUCKZLZBGKZACUGBUDZUHZMZADUENZNZFOZLZPZCFUFZFWEMZFWDCQWBWLWJ
      WBFGCQZWLVTVSEJKZWAWNEUIZCDEFGHIUJUKFGCULVHUMWKWEDRNKZWMWBWFWCWQWIWBWCWDE
      RNKZWQVTVSWCWRWABEGIUNUOVTVSWOWAWRWQWPWDCDEUPUQURUSVSVTWJWQWMSZWAVSVTPWFW
      IWSWCVSWFWIPZWSVTVSWTPWQWHWEMZWMVSWFWQXASWIVSWFPZWQXAXBWQPZWHWEWGNZWEXCVS
      WEFMZWFWHXDMVSWFWQUTVSWQXEWFWEDFHVATVSWFWQVBWEADFHVCVDVSWQXDWEOWFWEDVETVF
      VIVGWIXAWMVJVSWFWHFWEVKVLVMTVNVOVPFBCVQVR $.
      $( [15-Mar-2007] $)
  $}


