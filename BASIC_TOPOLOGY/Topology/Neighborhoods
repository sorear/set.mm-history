$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Neighborhoods
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c nei $.

  $( Extend class notation with neighborhood relation for topologies. $)
  cnei $a class nei $.

  ${
    $d g j x y $.
    $( Define a function on topologies whose value is a map from a subset to
       its neighborhoods. $)
    df-nei $a |- nei = ( j e. Top |-> ( x e. ~P U. j |->
                   { y e. ~P U. j | E. g e. j ( x C_ g /\ g C_ y ) } ) ) $.
  $}

  ${
    $d g j v x J $.  $d g v N $.  $d g P $.  $d g v x S $.  $d g j v x X $.
    neifval.1 $e |- X = U. J $.
    $( The neighborhood function on the subsets of a topology's base set. $)
    neifval $p |- ( J e. Top -> ( nei ` J ) = ( x e. ~P X |->
                   { v e. ~P X | E. g e. J ( x C_ g /\ g C_ v ) } ) ) $=
      ( vj ctop wcel cpw cv wss wa wrex crab cmpt cvv cnei wceq cuni cfv topopn
      pwexg mptexg 3syl unieq syl6eqr pweqd rexeq rabeqbidv df-nei fvmptg mpdan
      mpteq12dv ) DHIZAEJZAKCKZLUQBKLMZCDNZBUPOZPZQIZDRUAVASUOEDIUPQIVBDEFUBEDU
      CAUPUTQUDUEGDAGKZTZJZURCVCNZBVEOZPVAHQRVCDSZAVEVGUPUTVHVDEVHVDDTEVCDUFFUG
      UHZVHVFUSBVEUPVIURCVCDUIUJUNABCGUKULUM $.
      $( [11-Nov-2013] $) $( [11-Feb-2007] $)

    $( The neighborhood function is a function of the subsets of a topology's
       base set. $)
    neif $p |- ( J e. Top -> ( nei ` J ) Fn ~P X ) $=
      ( vx vg vv ctop wcel cnei cfv cpw wfn cv wss wa wrex crab cmpt cvv wral
      topopn pwexg rabexg 3syl ralrimivw eqid fnmpt syl neifval fneq1d mpbird )
      AGHZAIJZBKZLDUNDMEMZNUOFMNOEAPZFUNQZRZUNLZULUQSHZDUNTUSULUTDUNULBAHUNSHUT
      ABCUABAUBUPFUNSUCUDUEDUNUQURSURUFUGUHULUNUMURDFEABCUIUJUK $.
      $( [11-Nov-2013] $) $( [12-Feb-2007] $)

    $( A set with a neighborhood is a subset of the topology's base set.  (This
       theorem depends on a function's value being empty outside of its domain,
       but it will make later theorems simpler to state.) $)
    neiss2 $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) -> S C_ X ) $=
      ( ctop wcel cnei cfv wa cpw wss cdm elfvdm adantl wb syl adantr mpbid cvv
      wfn wceq neif fndm eleq2d cuni uniexg syl5eqel elpw2g ) BFGZCABHIZIGZJZAD
      KZGZADLZUMAUKMZGZUOULURUJCAUKNOUJURUOPULUJUQUNAUJUKUNUAUQUNUBBDEUCUNUKUDQ
      UERSUJUOUPPZULUJDTGUSUJDBUFTEBFUGUHADTUIQRS $.
      $( [12-Feb-2007] $)

    $( The set of neighborhoods of a subset of the base set of a topology. $)
    neival $p |- ( ( J e. Top /\ S C_ X ) -> ( ( nei ` J ) ` S ) =
                 { v e. ~P X | E. g e. J ( S C_ g /\ g C_ v ) } ) $=
      ( vx ctop wcel wss wa cnei cfv cpw cv wrex crab wceq adantr cvv fveq1d wb
      cmpt neifval topopn elpw2g biimpar pwexg rabexg 3syl sseq1 anbi1d rexbidv
      syl rabbidv eqid fvmptg syl2anc eqtrd ) DHIZBEJZKZBDLMZMZBGENZGOZCOZJZVGA
      OJZKZCDPZAVEQZUCZMZBVGJZVIKZCDPZAVEQZUTVDVNRVAUTBVCVMGACDEFUDUASVBBVEIZVR
      TIZVNVRRUTVSVAUTEDIZVSVAUBDEFUEZBEDUFUNUGUTVTVAUTWAVETIVTWBEDUHVQAVETUIUJ
      SGBVLVRVETVMVFBRZVKVQAVEWCVJVPCDWCVHVOVIVFBVGUKULUMUOVMUPUQURUS $.
      $( [11-Nov-2013] $) $( [11-Feb-2007] $)

    $( The predicate " ` N ` is a neighborhood of ` S ` ."  (Contributed by FL,
       25-Sep-2006.) $)
    isnei $p |- ( ( J e. Top /\ S C_ X ) -> ( N e. ( ( nei ` J ) ` S ) <->
                 ( N C_ X /\ E. g e. J ( S C_ g /\ g C_ N ) ) ) ) $=
      ( vv ctop wcel wss wa cnei cfv cv wrex cpw crab neival eleq2d wb wceq syl
      sseq2 anbi2d rexbidv elrab topopn elpw2g anbi1d syl5bb adantr bitrd ) CHI
      ZAEJZKZDACLMMZIDABNZJZUQGNZJZKZBCOZGEPZQZIZDEJZURUQDJZKZBCOZKZUOUPVDDGABC
      EFRSUMVEVJTUNVEDVCIZVIKUMVJVBVIGDVCUSDUAZVAVHBCVLUTVGURUSDUQUCUDUEUFUMVKV
      FVIUMECIVKVFTCEFUGDECUHUBUIUJUKUL $.
      $( [11-Nov-2013] $) $( [11-Feb-2007] $)

    $( An intuitive definition of a neighborhood in terms of interior.
       (Contributed by Szymon Jaroszewicz, 18-Dec-2007.) $)
    neiint $p |- ( ( J e. Top /\ S C_ X /\ N C_ X ) ->
           ( N e. ( ( nei ` J ) ` S ) <-> S C_ ( ( int ` J ) ` N ) ) ) $=
      ( vv ctop wcel wss w3a cnei cfv cv wa wrex cnt wb isnei 3adant3 syl2anc
      simp3 biantrurd simprrl ssntr 3adantl2 sstrd expr rexlimdva simpl1 simpl3
      adantrrl ntropn simpr ntrss2 wceq sseq2 sseq1 anbi12d rcla4ev syl12anc ex
      impbid 3bitr2d ) BGHZADIZCDIZJZCABKLLHZVFAFMZIZVICIZNZFBOZNZVMACBPLLZIZVD
      VEVHVNQVFAFBCDERSVGVFVMVDVEVFUAUBVGVMVPVGVLVPFBVGVIBHZVLVPVGVQVLNNAVIVOVG
      VQVJVKUCVGVQVKVIVOIZVJVDVFVQVKNVRVECBVIDEUDUEUKUFUGUHVGVPVMVGVPNZVOBHZVPV
      OCIZVMVSVDVFVTVDVEVFVPUIZVDVEVFVPUJZCBDEULTVGVPUMVSVDVFWAWBWCCBDEUNTVLVPW
      ANFVOBVIVOUOVJVPVKWAVIVOAUPVIVOCUQURUSUTVAVBVC $.
      $( [11-Nov-2013] $) $( [7-Sep-2007] $)

    $( The predicate " ` N ` is a neighborhood of point ` P ` ." $)
    isneip $p |- ( ( J e. Top /\ P e. X ) -> ( N e. ( ( nei ` J ) ` { P } ) <->
                 ( N C_ X /\ E. g e. J ( P e. g /\ g C_ N ) ) ) ) $=
      ( ctop wcel wa csn cnei cfv wss cv wrex wb snssi isnei sylan2 snssg
      anbi1d rexbidv anbi2d adantl bitr4d ) CGHZAEHZIDAJZCKLLHZDEMZUHBNZMZUKDMZ
      IZBCOZIZUJAUKHZUMIZBCOZIZUGUFUHEMUIUPPAEQUHBCDEFRSUGUTUPPUFUGUSUOUJUGURUN
      BCUGUQULUMAUKETUAUBUCUDUE $.
      $( [26-Feb-2007] $)

    $( A neighborhood is included in the topology's base set. $)
    neii1 $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) -> N C_ X ) $=
      ( vg ctop wcel cnei cfv wa wss neiss2 cv wrex isnei simpl syl6bi impancom
      mpd ) BGHZCABIJJHZKADLZCDLZABCDEMUAUCUBUDUAUCKUBUDAFNZLUECLKFBOZKUDAFBCDE
      PUDUFQRST $.
      $( [12-Feb-2007] $)
  $}

  ${
    $d g J $.  $d g N $.  $d g R $.  $d g S $.
    $( Property of a neighborhood. $)
    neii2 $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) ->
                 E. g e. J ( S C_ g /\ g C_ N ) ) $=
      ( ctop wcel cnei cfv wa cuni wss cv wrex eqid neiss2 isnei simpr impancom
      syl6bi mpd ) CEFZDACGHHFZIACJZKZABLZKUEDKIBCMZACDUCUCNZOUAUDUBUFUAUDIUBDU
      CKZUFIUFABCDUCUGPUHUFQSRT $.
      $( [12-Feb-2007] $)

    $( Any neighborhood of a set ` S ` is also a neighborhood of any subset
       ` R C_ S ` .  Bourbaki TG I.2.  (Contributed by FL, 25-Sep-2006.) $)
    neiss $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) /\ R C_ S ) ->
                 N e. ( ( nei ` J ) ` R ) ) $=
      ( vg ctop wcel cnei cfv wss w3a cuni cv wa wrex eqid neii1 3adant3 neii2
      wi sstr2 anim1d reximdv 3ad2ant3 mpd wb simp1 simp3 sstrd isnei mpbir2and
      neiss2 syl2anc ) CFGZDBCHIZIGZABJZKZDAUOIGZDCLZJZAEMZJZVBDJZNZECOZUNUPVAU
      QBCDUTUTPZQRURBVBJZVDNZECOZVFUNUPVJUQBECDSRUQUNVJVFTUPUQVIVEECUQVHVCVDABV
      BUAUBUCUDUEURUNAUTJUSVAVFNUFUNUPUQUGURABUTUNUPUQUHUNUPBUTJUQBCDUTVGULRUIA
      ECDUTVGUJUMUK $.
      $( [12-Feb-2007] $)

    $( A set is included in its neighborhoods.  Based on Bourbaki TG I.3 Viii.
       (Contributed by FL, 16-Nov-2006.) $)
    ssnei $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) -> S C_ N ) $=
      ( vg ctop wcel cnei cfv wa cv wss wrex neii2 sstr rexlimivw syl ) BEFCABG
      HHFIADJZKQCKIZDBLACKZADBCMRSDBAQCNOP $.
      $( [11-Feb-2007] $)
  $}

  $( A point belongs to any of its neighborhoods.  Based on Bourbaki TG I.3
     Viii.  (Contributed by FL, 28-Sep-2006.) $)
  elnei $p |- ( ( J e. Top /\ P e. A /\ N e. ( ( nei ` J ) ` { P } ) ) ->
              P e. N ) $=
    ( ctop wcel csn cnei cfv w3a wss ssnei 3adant2 wb snssg 3ad2ant2 mpbird ) C
    EFZBAFZDBGZCHIIFZJBDFZTDKZRUAUCSTCDLMSRUBUCNUABDAOPQ $.
    $( [11-Feb-2007] $)

  $( The empty set is not a neighborhood of a nonempty set.  (Contributed by
     FL, 18-Sep-2007.) $)
  0nnei $p |- ( ( J e. Top /\ S =/= (/) ) -> -. (/) e. ( ( nei ` J ) ` S ) ) $=
    ( ctop wcel c0 wne cnei cfv wn wceq wa wss ssnei ss0b sylib ex necon3ad imp
    ) BCDZAEFEABGHHDZISTAESTAEJZSTKAELUAABEMANOPQR $.
    $( [18-Sep-2007] $)

  ${
    $d g h p v J $.  $d g M $.  $d g h p v N $.  $d g P $.  $d g h p S $.
    $d g h p X $.
    neips.1 $e |- X = U. J $.
    $( A neighborhood of a set is a neighborhood of every point in the set.
       Bourbaki TG I.2.  (Contributed by FL, 16-Nov-2006.) $)
    neips $p |- ( ( J e. Top /\ S C_ X /\ S =/= (/) ) ->
                ( N e. ( ( nei ` J ) ` S ) <->
                A. p e. S N e. ( ( nei ` J ) ` { p } ) ) ) $=
      ( vg vh vv wcel wss cfv cv wral wi wa wrex wb sseq1 3adant3 ctop wne cnei
      w3a csn snssi neiss syl3an3 3exp ralrimdv 3ad2ant1 r19.28zv 3ad2ant3 crab
      ssrab2 uniopn mpan2 ad2antrr elrab elunii sylan2br an12s rexlimiva ralimi
      c0 dfss3 sylibr adantl unissb simprbi mprgbir jctir sseq2 anbi12d rcla4ev
      cuni wceq syl2anc ex anim2d sylbid isneip sylan2 anassrs ralbidva 3imtr4d
      ssel2 isnei impbid ) BUAJZADKZAVEUBZUDZCABUCLZLJZCEMZUEZWNLJZEANZWJWKWOWS
      OWLWJWOWREAWJWOWPAJZWRWTWJWOWQAKWRWPAUFWQABCUGUHUIUJUKWMCDKZWPGMZJZXBCKZP
      ZGBQZPZEANZXAAHMZKZXICKZPZHBQZPZWSWOWMXHXAXFEANZPZXNWLWJXHXPRWKXAXFEAULUM
      WJWKXPXNOWLWJWKPZXOXMXAXQXOXMXQXOPZIMZCKZIBUNZVPZBJZAYBKZYBCKZPZXMWJYCWKX
      OWJYABKYCXTIBUOYABUPUQURXRYDYEXOYDXQXOWPYBJZEANYDXFYGEAXEYGGBXCXBBJZXDYGY
      HXDPXCXBYAJYGXTXDIXBBXSXBCSUSWPXBYAUTVAVBVCVDEAYBVFVGVHYEXKHYAHYACVIXIYAJ
      XIBJXKXTXKIXIBXSXICSUSVJVKVLXLYFHYBBXIYBVQXJYDXKYEXIYBAVMXIYBCSVNVOVRVSVT
      TWAWJWKWSXHRWLXQWRXGEAWJWKWTWRXGRZWKWTPWJWPDJYIADWPWGWPGBCDFWBWCWDWETWJWK
      WOXNRWLAHBCDFWHTWFWI $.
      $( [11-Feb-2007] $)

    $( An open set is a neighborhood of any of its subsets.  (Contributed by
       FL, 2-Oct-2006.) $)
    opnneissb $p |- ( ( J e. Top /\ N e. J /\ S C_ X ) ->
                   ( S C_ N <-> N e. ( ( nei ` J ) ` S ) ) ) $=
      ( vg ctop wcel wss w3a cnei cfv wi wa cv wrex eltopss adantr ssid wceq wb
      sseq2 sseq1 anbi12d rcla4ev mpanr2 ad2ant2l isnei ad2ant2r mpbir2and 3imp
      exp43 ssnei ex 3ad2ant1 impbid ) BGHZCBHZADIZJACIZCABKLLHZUQURUSUTVAMUQUR
      USUTVAUQURNZUSUTNZNVACDIZAFOZIZVECIZNZFBPZVBVDVCCBDEQRURUTVIUQUSURUTCCIZV
      ICSVHUTVJNFCBVECTVFUTVGVJVECAUBVECCUCUDUEUFUGUQUSVAVDVINUAURUTAFBCDEUHUIU
      JULUKUQURVAUTMUSUQVAUTABCUMUNUOUP $.
      $( [12-Feb-2007] $)

    $( Any superset of an open set is a neighborhood of it. $)
    opnssneib $p |- ( ( J e. Top /\ S e. J /\ N C_ X ) ->
                   ( S C_ N <-> N e. ( ( nei ` J ) ` S ) ) ) $=
      ( vg ctop wcel wss w3a cnei cfv cv wa wrex wi simplr wceq sseq2 ex jca wb
      sseq1 anbi12d ssid biantrur syl6bbr rcla4ev adantlr 3adant1 eltopss isnei
      syldan 3adant3 sylibrd ssnei 3ad2ant1 impbid ) BGHZABHZCDIZJZACIZCABKLLHZ
      VBVCVAAFMZIZVECIZNZFBOZNZVDUTVAVCVJPUSUTVANZVCVJVKVCNVAVIUTVAVCQUTVCVIVAV
      HVCFABVEARZVHAAIZVCNVCVLVFVMVGVCVEAASVEACUCUDVMVCAUEUFUGUHUIUATUJUSUTVDVJ
      UBZVAUSUTADIVNABDEUKAFBCDEULUMUNUOUSUTVDVCPVAUSVDVCABCUPTUQUR $.
      $( [14-Feb-2007] $)

    $( Any subset of ` X ` containing a neigborhood of a set is a neighborhood
       of this set.  Based on Bourbaki TG I.3 Vi.  (Contributed by FL,
       2-Oct-2006.) $)
    ssnei2 $p |- ( ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) /\
                 ( N C_ M /\ M C_ X ) ) -> M e. ( ( nei ` J ) ` S ) ) $=
      ( vg ctop wcel cnei cfv wa wss cv wrex simprr neii2 sstr2 com12 anim2d wb
      reximdv mpan9 adantrr neiss2 isnei syldan adantr mpbir2and ) BHIZDABJKKZI
      ZLZDCMZCEMZLZLCUKIZUOAGNZMZURCMZLZGBOZUMUNUOPUMUNVBUOUMUSURDMZLZGBOUNVBAG
      BDQUNVDVAGBUNVCUTUSVCUNUTURDCRSTUBUCUDUMUQUOVBLUAZUPUJULAEMVEABDEFUEAGBCE
      FUFUGUHUI $.
      $( [13-Feb-2007] $)

    $( Any neighborhood of an element in the closure of a subset intersects the
       subset.  Part of proof of Theorem 6.6 of [Munkres] p. 97. $)
    neindisj $p |- ( ( ( J e. Top /\ S C_ X ) /\ ( P e. ( ( cls ` J ) ` S ) /\
                   N e. ( ( nei ` J ) ` { P } ) ) ) -> ( N i^i S ) =/= (/) ) $=
      ( vg ctop wcel wss ccl cfv csn cin c0 wne wi wa wceq ex cnei cv wb clsss3
      wrex sseld impr isneip syldan w3a 3anass clsndisj sylanbr anassrs adantrr
      adantllr ssdisj necon3d ad2antll mpd rexlimdva expimpd sylbid exp32 imp43
      ) CHIZBEJZABCKLLZIZDAMCUALLIZDBNZOPZVFVGVIVJVLQVFVGVIRZRZVJDEJZAGUBZIZVPD
      JZRZGCUEZRZVLVFVMAEIZVJWAUCVFVGVIWBVFVGRVHEABCEFUDUFUGAGCDEFUHUIVNVOVTVLV
      NVORZVSVLGCWCVPCIZRZVSVLWEVSRVPBNZOPZVLWEVQWGVRVNWDVQWGVOVNWDVQWGVNVFVGVI
      UJWDVQRWGVFVGVIUKABVPCEFULUMUNUPUOVRWGVLQWEVQVRVKOWFOVRVKOSWFOSVPDBUQTURU
      SUTTVAVBVCVDVE $.
      $( [26-Feb-2007] $)
  $}

  $( An open set is a neighborhood of any of its subsets. $)
  opnneiss $p |- ( ( J e. Top /\ N e. J /\ S C_ N ) ->
                  N e. ( ( nei ` J ) ` S ) ) $=
    ( ctop wcel wss w3a cnei cfv simp3 cuni wb wa eqid eltopss sstr sylan 3impa
    ancoms opnneissb syld3an3 mpbid ) BDEZCBEZACFZGUECABHIIEZUCUDUEJUCUDUEABKZF
    ZUEUFLUCUDUEUHUCUDMCUGFZUEUHCBUGUGNZOUEUIUHACUGPSQRABCUGUJTUAUB $.
    $( [13-Feb-2007] $)

  $( An open set is a neighborhood of any of its members. $)
  opnneip $p |- ( ( J e. Top /\ N e. J /\ P e. N ) ->
                  N e. ( ( nei ` J ) ` { P } ) ) $=
    ( wcel ctop csn wss cnei cfv snssi opnneiss syl3an3 ) ACDBEDCBDAFZCGCMBHIID
    ACJMBCKL $.
    $( [8-Mar-2007] $)

  ${
    $d x J $.  $d x S $.
    $( A set is open iff it is a neighborhood of all its points.  ( Contributed
       by Jeff Hankins, 15-Sep-2009.) $)
    opnnei $p |- ( J e. Top -> ( S e. J <-> A. x e. S S e. ( (
    nei ` J ) ` { x } ) ) ) $=
      ( ctop wcel c0 wceq cv csn cnei cfv wral wb wa adantr adantl wi wss snss
      ex 0opn eleq1 mpbird rzal 2thd wn opnneip 3expia ralrimiv cuni wrex df-ne
      wne r19.2z sylbir neii1 rexlimdvw sylan9r cnt ntrss2 ralbii dfss3 biimpri
      eqid vex sylan2br eqssd sstr2 com12 syl5bi imp neiint 3com23 3expa syldan
      ralbidva isopn3 3imtr4d com23 mpdd impbid pm2.61dan ) CDEZBFGZBCEZBAHZIZC
      JKKEZABLZMWCWDNZWEWIWJWEFCEZWCWKWDCUAOWDWEWKMWCBFCUBPUCWDWIWCWHABUDPUEWCW
      DUFZNZWEWIWCWEWIQWLWCWEWIWCWENWHABWCWEWFBEZWHWFCBUGUHUITOWMWIBCUJZRZWEWLW
      IWHABUKZWCWPWLBFUMZWIWQQBFULWRWIWQWHABUNTUOWCWHWPABWCWHWPWGCBWOWOVDZUPTUQ
      URWCWIWPWEQQWLWCWPWIWEWCWPWIWEQWCWPNZWGBCUSKKZRZABLZXABGZWIWEWTXCXDWTXCNX
      ABWTXABRXCBCWOWSUTOXCWTWFXAEZABLZBXARZXEXBABWFXAAVEZSVAXFXGWTXGXFABXAVBVC
      PVFVGTWTWHXBABWTWNWGWORZWHXBMZWTWNXIWNWGBRZWTXIWFBXHSWPXKXIQWCXKWPXIWGBWO
      VHVIPVJVKWCWPXIXJWCXIWPXJWGCBWOWSVLVMVNVOVPBCWOWSVQVRTVSOVTWAWB $.
      $( [16-Sep-2009] $)
  $}

  ${
    $d J x $.  $d S x $.  $d X x $.
    tpnei.1 $e |- X = U. J $.
    $( The underlying set of a topology is a neighborhood of any of its
       subsets.  Special case of ~ opnneiss .  (Contributed by FL,
       2-Oct-2006.) $)
    tpnei $p |- ( J e. Top -> ( S C_ X <-> X e. ( ( nei ` J ) ` S ) ) ) $=
      ( ctop wcel wss cnei cfv wi topopn opnneiss 3exp mpd ssnei ex impbid ) BE
      FZACGZCABHIIFZRCBFZSTJBCDKRUASTABCLMNRTSABCOPQ $.
      $( [14-Feb-2007] $)

    $( The union of the neighborhoods of a set equals the topology's underlying
       set.  (Contributed by FL, 18-Sep-2007.) $)
    unnei $p |- ( ( J e. Top /\ S C_ X ) -> U. ( ( nei ` J ) ` S ) = X ) $=
      ( vx ctop wcel wss wa cnei cfv cuni wceq cv wral wi neii1 adantr ralrimiv
      ex unissb sylibr tpnei biimpa unissel syl2anc ) BFGZACHZIZABJKKZLZCHZCUJG
      ZUKCMUIENZCHZEUJOULUIUOEUJUGUNUJGZUOPUHUGUPUOABUNCDQTRSEUJCUAUBUGUHUMABCD
      UCUDUJCUEUF $.
      $( [18-Sep-2007] $)
  $}

  ${
    $d g h v x y J $.  $d g h v M $.  $d g h v x y N $.  $d g h v x y S $.
    $( The intersection of two neighborhoods of a set is also a neighborhood of
       the set.  Based on Bourbaki TG I.3 Vii.  (Contributed by FL,
       28-Sep-2006.) $)
    innei $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S )
   /\ M e. ( ( nei ` J ) ` S ) ) -> ( N i^i M ) e. ( ( nei ` J ) ` S ) ) $=
      ( vg vh vv ctop wcel cfv cin cv wa wrex 3adant3 neii2 wi rexlimdva syldan
      wss cnei w3a cuni eqid neii1 ssinss1 syl anim12dan inopn 3expa ssin ss2in
      biimpi anim12i an4s wceq sseq2 sseq1 anbi12d rcla4ev syl2an expr an32s ex
      imp32 3impb wb neiss2 isnei mpbir2and ) BHIZDABUAJJZIZCVLIZUBDCKZVLIZVOBU
      CZTZAELZTZVSVOTZMZEBNZVKVMVRVNVKVMMDVQTVRABDVQVQUDZUEDCVQUFUGOVKVMVNWCVKV
      MVNMAFLZTZWEDTZMZFBNZAGLZTZWJCTZMZGBNZMWCVKVMWIVNWNAFBDPAGBCPUHVKWIWNWCVK
      WHWNWCQZFBVKWEBIZMZWHWOWQWHMWMWCGBWQWJBIZWHWMWCQWQWRMZWHWMWCWSWEWJKZBIZAW
      TTZWTVOTZMZWCWHWMMVKWPWRXAWEWJBUIUJWFWKWGWLXDWFWKMZXBWGWLMXCXEXBAWEWJUKUM
      WEDWJCULUNUOWBXDEWTBVSWTUPVTXBWAXCVSWTAUQVSWTVOURUSUTVAVBVCRVDRVESVFVKVMV
      PVRWCMVGZVNVKVMAVQTXFABDVQWDVHAEBVOVQWDVISOVJ $.
      $( [12-Feb-2007] $)

    $( Only an open set is a neighborhood of itself.  (Contributed by FL,
       2-Oct-2006.) $)
    opnneiid $p |- ( J e. Top -> ( N e. ( ( nei ` J ) ` N ) <-> N e. J ) ) $=
      ( vx ctop wcel cnei cfv wa cv wss wrex neii2 wceq eleq1a syl5bir rexlimiv
      eqss syl ex ssid opnneiss 3exp mpii impbid ) ADEZBBAFGGEZBAEZUEUFUGUEUFHB
      CIZJUHBJHZCAKUGBCABLUIUGCAUIBUHMUHAEUGBUHQUHABNOPRSUEUGBBJZUFBTUEUGUJUFBA
      BUAUBUCUD $.
      $( [13-Feb-2007] $)

    $( For any neighborhood ` N ` of ` S ` , there is a neighborhood ` x ` of
       ` S ` such that ` N ` is a neighborhood of all subsets of ` x ` .  Based
       on Bourbaki TG I.3 Viv.  (Contributed by FL, 2-Oct-2006.) $)
    neissex $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) ->
   E. x e. ( ( nei ` J ) ` S ) A. y ( y C_ x -> N e. ( ( nei ` J ) ` y ) ) ) $=
      ( ctop wcel cnei cfv wa cv wss wrex wi wal neii2 simpr adantr syl3anc ex
      opnneiss 3expb adantrrr adantlr simplll cuni simpll eqid opnssneib biimpa
      wb neii1 anasss neiss adantrrl alrimiv jca reximdv2 mpd ) DFGZECDHIZIZGZJ
      ZCAKZLZVEELZJZADMBKZVELZEVIVAIGZNZBOZAVBMCADEPVDVHVMADVBVDVEDGZVHJZVEVBGZ
      VMJVDVOJZVPVMUTVOVPVCUTVNVFVPVGUTVNVFVPCDVEUAUBUCUDVQVLBVDVNVGVLVFVDVNVGJ
      ZJZVJVKVSVJJUTEVEVAIGZVJVKUTVCVRVJUEVSVTVJVDVNVGVTVDVNJZVGVTWAUTVNEDUFZLZ
      VGVTUKUTVCVNUGVDVNQVDWCVNCDEWBWBUHZULRVEDEWBWDUISUJUMRVSVJQVIVEDEUNSTUOUP
      UQTURUS $.
      $( [13-Feb-2007] $)
  $}

  $( The empty set is a neighborhood of itself.  (Contributed by FL,
     10-Dec-2006.) $)
  0nei $p |- ( J e. Top -> (/) e. ( ( nei ` J ) ` (/) ) ) $=
    ( ctop wcel c0 cnei cfv 0opn opnneiid mpbird ) ABCDDAEFFCDACAGADHI $.
    $( [14-Feb-2007] $)


