$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Product topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c tX $.

  $( Extend class notation with the binary topological product operation. $)
  ctx $a class tX $.

  ${
    $d r s x y z $.
    $( Define the binary topological product, which is homeomorphic to the
       general topological product over a two element set, but is more
       convenient to use.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    df-tx $a |- tX = ( r e. Top , s e. Top |->
             ( topGen ` { x | E. y e. r E. z e. s x = ( y X. z ) } ) ) $.
  $}

  ${
    $d R r s t u v z $.  $d S r s t u v z $.
    $( Value of the binary topological product operation.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    txval $p |- ( ( R e. Top /\ S e. Top ) -> ( R tX S ) =
                  ( topGen ` { z | E. u e. R E. v e. S z = ( u X. v ) } ) ) $=
      ( vr vs ctop cv cxp wceq wrex cab ctg cfv ctx rexeq abbidv fveq2d rexbidv
      df-tx fvex ovmpt2 ) FGDEHHAICIBIJKZBGIZLZCFIZLZAMZNOUDBELZCDLZAMZNOPUFCDL
      ZAMZNOUGDKZUIUNNUOUHUMAUFCUGDQRSUEEKZUNULNUPUMUKAUPUFUJCDUDBUEEQTRSACBGFU
      AULNUBUC $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d R a b c d r s u v x y z $.  $d S a b c d r s u v x y z $.
    $( A lemma for theorems about product topologies.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    txbas $p |- ( ( R e. Top /\ S e. Top ) ->
                  { z | E. u e. R E. v e. S z = ( u X. v ) } e. TopBases ) $=
      ( vx vy va vb vd wcel wa cv cxp wceq wrex cvv vex wi eqeq2d vs vr vc ctop
      cab cin wral ctb rexeq abbidv eleq1d rexbidv ab2rexex eqeq1 2rexbidv elab
      vtocl2g anbi12i xpeq2 cbvrexv rexbii xpeq1 bitri ancom2s adantlr adantlrr
      inopn 3expb anassrs adantrr adantll adantlrl adantrl a1i rcla42ev syl3anc
      inxp xpex inex1 sylibr ineq1 syl5ibrcom ineq2 biimprcd ex com24 rexlimdvv
      syl6 syl5bi imp3a ralrimivv fiinbas syl2anc ) DUDKZEUDKZLZAMZCMZBMZNZOZBE
      PZCDPZAUEZQKZFMZGMZUFZXDKZGXDUGFXDUGXDUHKXABUAMZPZCUBMZPZAUEZQKXKCDPZAUEZ
      QKXEUBUADEUDUDXLDOZXNXPQXQXMXOAXKCXLDUIUJUKXJEOZXPXDQXRXOXCAXRXKXBCDXABXJ
      EUIULUJUKCBAXLXJWTUBRUARUMUQWPXIFGXDXDXFXDKZXGXDKZLXFWTOZBEPZCDPZXGWTOZBE
      PZCDPZLWPXIXSYCXTYFXCYCAXFFRWQXFOXAYACBDEWQXFWTUNUOUPXCYFAXGGRWQXGOXAYDCB
      DEWQXGWTUNUOUPURWPYCYFXIYCXFHMZIMZNZOZIEPZHDPZWPYFXISZYCXFWRYHNZOZIEPZCDP
      YLYBYPCDYAYOBIEWSYHOWTYNXFWSYHWRUSTUTVAYPYKCHDWRYGOZYOYJIEYQYNYIXFWRYGYHV
      BTULUTVCWPYJYMHIDEWPYFYJYGDKZYHEKZLZXIYFXGUCMZJMZNZOZJEPZUCDPZWPYJYTXISSZ
      YFXGWRUUBNZOZJEPZCDPUUFYEUUJCDYDUUIBJEWSUUBOWTUUHXGWSUUBWRUSTUTVAUUJUUECU
      CDWRUUAOZUUIUUDJEUUKUUHUUCXGWRUUAUUBVBTULUTVCWPUUDUUGUCJDEWPUUADKZUUBEKZL
      ZUUDUUGSWPUUNLZYTYJUUDXIUUOYTYJUUDXISZSUUOYTLZYJXFUUCUFZXDKZUUPUUQUUSYJYI
      UUCUFZXDKZUUQUUTWTOZBEPCDPZUVAUUQYGUUAUFZDKZYHUUBUFZEKZUUTUVDUVFNZOZUVCUU
      OYRUVEYSWPUULYRUVEUUMWPUULYRUVEWNUULYRLUVEWOWNYRUULUVEWNYRUULUVEYGUUADVGV
      HVDVEVIVFVJUUOYSUVGYRWPUUMYSUVGUULWPUUMYSUVGWOUUMYSLUVGWNWOYSUUMUVGWOYSUU
      MUVGYHUUBEVGVHVDVKVIVLVMUVIUUQYGYHUUAUUBVQVNUVBUVIUUTUVDWSNZOCBUVDUVFDEWR
      UVDOWTUVJUUTWRUVDWSVBTWSUVFOUVJUVHUUTWSUVFUVDUSTVOVPXCUVCAUUTYIUUCYGYHHRI
      RVRVSWQUUTOXAUVBCBDEWQUUTWTUNUOUPVTYJUURUUTXDXFYIUUCWAUKWBUUDXIUUSUUDXHUU
      RXDXGUUCXFWCUKWDWHWEWFWEWGWIWFWGWIWJWIWKFGXDQWLWM $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d J u v x z $.  $d K u v x z $.
    eltpt.1 $e |- B = { z | E. u e. J E. v e. K z = ( u X. v ) } $.
    $( Condition for a set to belong to a product topology..  (Contributed by
       FL, 18-Apr-2010.) $)
    eltpt $p |- ( ( J e. Top /\ K e. Top ) -> ( A e. ( J tX K ) <-> E. x
       ( x C_ B /\ A = U. x ) ) ) $=
      ( ctop wcel wa ctx co cv wceq wrex wss wex wb cxp cab ctg cfv cuni eleq2d
      txval ctb txbas eltg3 syl sseq2i bicomi anbi1i exbii a1i 3bitrd ) GJKHJKL
      ZEGHMNZKEBODOCOUAPCHQDGQBUBZUCUDZKZAOZUTRZEVCUEPZLZASZVCFRZVELZASZURUSVAE
      BCDGHUGUFURUTUHKVBVGTBCDGHUIAEUTUJUKVGVJTURVFVIAVDVHVEVHVDFUTVCIULUMUNUOU
      PUQ $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d R u v z $.  $d S u v z $.  $d T u v z $.
    $( The product of two topologies is a topology.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    txtop $p |- ( ( R e. Top /\ S e. Top ) -> ( R tX S ) e. Top ) $=
      ( vz vu vv ctop wcel wa ctx co cxp wceq wrex cab ctg cfv txval ctb txbas
      cv tgcl syl eqeltrd ) AFGBFGHZABIJCTDTETKLEBMDAMCNZOPZFCEDABQUDUERGUFFGCE
      DABSUEUAUBUC $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    txtopi.1 $e |- R e. Top $.
    txtopi.2 $e |- S e. Top $.
    $( The product of two topologies is a topology.  (Contributed by Jeff
       Madsen, 15-Jun-2011.) $)
    txtopi $p |- ( R tX S ) e. Top $=
      ( ctop wcel ctx co txtop mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [15-Jun-2010] $)
  $}

  ${
    $d R u v x z $.  $d S u v x z $.  $d T u v x z $.  $d X u v x z $.
    $d Y u v x z $.
    txuni.1 $e |- X = U. R $.
    txuni.2 $e |- Y = U. S $.
    $( The underlying set of the product of two topologies.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    txuni $p |- ( ( R e. Top /\ S e. Top ) -> ( X X. Y ) = U. ( R tX S ) ) $=
      ( vz vu vv vx ctop wcel wa cuni cv cxp wceq wrex wss cvv ctx co cab txval
      ctg cfv unieqd ctb txbas unitg syl wral vex eqeq1 2rexbidv elab wi xpss12
      eltopss syl2an an4s ex sseq1 biimprcd syl6 rexlimdvv syl5bi unissb sylibr
      ralrimiv topopn adantr adantl eqidd xpeq1 eqeq2d xpeq2 rcla42ev wb uniexg
      syl3anc syl5eqel xpexg elabg mpbird elssuni eqssd 3eqtrrd ) AKLZBKLZMZABU
      AUBZNGOZHOZIOZPZQZIBRHARZGUCZUEUFZNZWSNZCDPZWKWLWTGIHABUDUGWKWSUHLXAXBQGI
      HABUIWSUJUKWKXBXCWKJOZXCSZJWSULXBXCSWKXEJWSXDWSLXDWPQZIBRHARZWKXEWRXGGXDJ
      UMWMXDQWQXFHIABWMXDWPUNUOUPWKXFXEHIABWKWNALZWOBLZMZWPXCSZXFXEUQWKXJXKWIXH
      WJXIXKWIXHMWNCSWODSXKWJXIMWNACEUSWOBDFUSWNCWODURUTVAVBXFXEXKXDWPXCVCVDVEV
      FVGVJJWSXCVHVIWKXCWSLZXCXBSWKXLXCWPQZIBRHARZWKCALZDBLZXCXCQZXNWIXOWJACEVK
      VLWJXPWIBDFVKVMWKXCVNXMXQXCCWOPZQHICDABWNCQWPXRXCWNCWOVOVPWODQXRXCXCWODCV
      QVPVRWAWKXCTLZXLXNVSWICTLDTLXSWJWICANTEAKVTWBWJDBNTFBKVTWBCDTTWCUTWRXNGXC
      TWMXCQWQXMHIABWMXCWPUNUOWDUKWEXCWSWFUKWGWH $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    txunii.1 $e |- R e. Top $.
    txunii.2 $e |- S e. Top $.
    txunii.3 $e |- X = U. R $.
    txunii.4 $e |- Y = U. S $.
    $( The underlying set of the product of two topologies.  (Contributed by
       Jeff Madsen, 15-Jun-2011.) $)
    txunii $p |- ( X X. Y ) = U. ( R tX S ) $=
      ( ctop wcel cxp ctx co cuni wceq txuni mp2an ) AIJBIJCDKABLMNOEFABCDGHPQ
      $.
      $( [15-Jun-2010] $)
  $}

  ${
    $d R u v z $.  $d S u v z $.  $d T u v z $.  $d A u v z $.  $d B u v z $.
    $( The product of two open sets is open in the product topology.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    txopn $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. R /\ B e. S ) )
                                               -> ( A X. B ) e. ( R tX S ) ) $=
      ( vz vu vv ctop wcel wa cxp cv wceq wrex cab ctg syl adantr eqeq2d cvv co
      cfv ctx wss ctb txbas bastg xpeq1 xpeq2 rcla42ev mp3an3 wb xpexg 2rexbidv
      eqid eqeq1 elabg mpbird adantl sseldd txval eleqtrrd ) CHIDHIJZACIZBDIZJZ
      JZABKZELZFLZGLZKZMZGDNFCNZEOZPUBZCDUCUAZVGVOVPVHVCVOVPUDZVFVCVOUEIVREGFCD
      UFVOUGQRVFVHVOIZVCVFVSVHVLMZGDNFCNZVDVEVHVHMZWAVHUOVTWBVHAVKKZMFGABCDVJAM
      VLWCVHVJAVKUHSVKBMWCVHVHVKBAUISUJUKVFVHTIVSWAULABCDUMVNWAEVHTVIVHMVMVTFGC
      DVIVHVLUPUNUQQURUSUTVCVQVPMVFEGFCDVARVB $.
      $( [2-Sep-2009] $)
  $}

  $( The product of two closed sets is closed in the product topology.
     (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  txcld $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. ( Clsd ` R )
           /\ B e. ( Clsd ` S ) ) ) -> ( A X. B ) e. ( Clsd ` ( R tX S ) ) ) $=
    ( ctop wcel wa ccld cfv cxp cuni wss cdif eqid adantr ex topopn txopn iscld
    anassrs ctx co xpss12 ad2ant2r adantl wceq txuni sseqtrd cun difxp syl5reqr
    difeq1d txtop ad2antlr mpdan adantrr ad2antrr ancom2s adantrl unopn syl3anc
    eqeltrd adantrll adantrrl jcad bi2anan9 wb syl 3imtr4d imp ) CEFZDEFZGZACHI
    FZBDHIFZGZABJZCDUAUBZHIFZVMACKZLZVTAMZCFZGZBDKZLZWEBMZDFZGZGZVQVRKZLZWKVQMZ
    VRFZGZVPVSVMWJWLWNVMWJWLVMWJGVQVTWEJZWKWJVQWPLZVMWAWFWQWCWHAVTBWEUCUDUEVMWP
    WKUFZWJCDVTWEVTNZWENZUGZOUHPVMWJWNVMWDWHWNWFVMWCWHWNWAVMWCWHGZGZWMWBWEJZVTW
    GJZUIZVRXCXFWPVQMWMABVTWEUJXCWPWKVQVMWRXBXAOULUKXCVREFZXDVRFZXEVRFZXFVRFVMX
    GXBCDUMZOVMWCXHWHVMWCGWEDFZXHVLXKVKWCDWEWTQUNVMWCXKXHWBWECDRTUOUPVMWHXIWCVM
    WHGVTCFZXIVKXLVLWHCVTWSQUQVMWHXLXIVMXLWHXIVTWGCDRURTUOUSXDXEVRUTVAVBVCVDPVE
    VKVNWDVLVOWIACVTWSSBDWEWTSVFVMXGVSWOVGXJVQVRWKWKNSVHVIVJ $.
    $( [26-Jun-2014] $) $( [2-Sep-2009] $)

  ${
    $d R u v w x z $.  $d S u v w x z $.  $d T u v w x z $.  $d X u v w x z $.
    $d Y u v w x z $.  $d Z u v w x z $.
    tx1cn.1 $e |- X = U. R $.
    tx1cn.2 $e |- Y = U. S $.
    tx1cn.3 $e |- Z = ( X X. Y ) $.
    $( Continuity of the first projection map of a topological product.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    tx1cn $p |- ( ( R e. Top /\ S e. Top ) ->
                  ( 1st |` Z ) e. ( ( R tX S ) Cn R ) ) $=
      ( vz vu vv ctop wcel wa c1st cxp wceq wss cvv wi vw cres ctx co cuni ccnv
      ccn wf cv cima wral wb txtop simpl eqid syl2anc f1stres a1i reseq2d feq1d
      iscn txuni feq2d bitr4d mpbiri wrex cab ctg cfv ctb txbas bastg syl txval
      sseqtr4d adantr simpr topopn reseq2i cnveqi imaeq1i crn imassrn sseli cdm
      ad2antlr cin dfdm4 dmres eqtr3i wfo fo1st fof fdm mp2b ineq2i inv1 3eqtri
      syl6eleq wfun ffun ax-mp fvimacnvi mpan fvres eleq1d biimpd xp2nd biimpri
      c2nd xpss elxp7 syl12anc ex syldd com23 syl5 mpdi ssrdv eltopss xpss1 imp
      sseld adantllr xp1st adantl eqeltrd ralrimiva adantlr syl6sseqr funimass4
      fdmi sylancr mpbird funimass3 mpbid eqssd eqeq2d uniexg syl5eqel rcla42ev
      syl5eq xpeq1 xpeq2 syl3anc xpexg syl2an cnvexg imaexg 3syl eqeq1 2rexbidv
      fex elabg sseldd mpbir2and ) ALMZBLMZNZOEUBZABUCUDZAUGUDMZUVAUEZCUUTUHZUU
      TUFZUAUIZUJZUVAMZUAAUKZUUSUVALMUUQUVBUVDUVINULABUMUUQUURUNUAUUTUVAAUVCCUV
      CUOFVAUPUUSUVDCDPZCOUVJUBZUHZCDUQZUUSUVDUVCCUVKUHUVLUUSUVCCUUTUVKUUSEUVJO
      EUVJQUUSHURUSUTUUSUVJUVCCUVKABCDFGVBVCVDVEUUSUVHUAAUUSUVFAMZNZIUIZJUIZKUI
      ZPZQZKBVFJAVFZIVGZUVAUVGUUSUWBUVARUVNUUSUWBUWBVHVIZUVAUUSUWBVJMUWBUWCRIKJ
      ABVKUWBVLVMIKJABVNVOVPUVOUVGUWBMZUVGUVSQZKBVFJAVFZUVOUVNDBMZUVGUVFDPZQZUW
      FUUSUVNVQUURUWGUUQUVNBDGVRWFUVOUVGUVKUFZUVFUJZUWHUVEUWJUVFUUTUVKEUVJOHVSZ
      VTWAUVOUWKUWHUVOIUWKUWHUVOUVPUWKMZUVPUVJMZUVPUWHMZUWMUVPUWJWBZUVJUWKUWPUV
      PUWJUVFWCWDUWPUVJOWEZWGZUVJSWGUVJUVKWEZUWPUWRUVKWHOUVJWIWJUWQSUVJSSOWKSSO
      UHUWQSQWLSSOWMSSOWNWOWPUVJWQWRWSUWMUVPUVKVIZUVFMZUVOUWNUWOTUVKWTZUWMUXAUV
      LUXBUVMUVJCUVKXAXBZUVPUVFUVKXCXDUVOUWNUXAUWOUVOUWNUXAUVPOVIZUVFMZUWOUWNUX
      AUXETTUVOUWNUXAUXEUWNUWTUXDUVFUVPUVJOXEZXFXGURUWNUXEUWOTTUVOUWNUXEUWOUWNU
      XENUVPSSPZMZUXEUVPXJVIDMZUWOUWNUXHUXEUVJUXGUVPCDXKWDVPUWNUXEVQUWNUXIUXEUV
      PCDXHVPUWOUXHUXEUXINNUVPUVFDXLXIXMXNURXOXPXQXRXSUVOUVKUWHUJUVFRZUWHUWKRZU
      VOUXJUXAIUWHUKZUVOUXAIUWHUVOUWONZUWTUXDUVFUXMUWNUWTUXDQUUQUVNUWOUWNUURUUQ
      UVNNZUWOUWNUXNUWHUVJUVPUXNUVFCRUWHUVJRZUVFACFXTUVFCDYAVMZYCYBYDUXFVMUWOUX
      EUVOUVPUVFDYEYFYGYHUVOUXBUWHUWSRZUXJUXLULUXCUVOUWHUVJUWSUUQUVNUXOUURUXPYI
      UVJCUVKUVMYLYJZIUWHUVFUVKYKYMYNUVOUXBUXQUXJUXKULUXCUXRUWHUVFUVKYOYMYPYQUU
      BUWEUWIUVGUVFUVRPZQJKUVFDABUVQUVFQUVSUXSUVGUVQUVFUVRUUCYRUVRDQUXSUWHUVGUV
      RDUVFUUDYRUUAUUEUVOUVGSMZUWDUWFULUVOUUTSMUVESMUXTUVOUUTUVKSUWLUUSUVKSMZUV
      NUUSUVLUVJSMZUYAUVMUUQCSMDSMUYBUURUUQCAUESFALYSYTUURDBUESGBLYSYTCDSSUUFUU
      GUVJCSUVKUUMYMVPYTUUTSUUHUVEUVFSUUIUUJUWAUWFIUVGSUVPUVGQUVTUWEJKABUVPUVGU
      VSUUKUULUUNVMYNUUOYHUUP $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d R u v w x z $.  $d S u v w x z $.  $d T u v w x z $.  $d X u v w x z $.
    $d Y u v w x z $.  $d Z u v w x z $.
    tx2cn.1 $e |- X = U. R $.
    tx2cn.2 $e |- Y = U. S $.
    tx2cn.3 $e |- Z = ( X X. Y ) $.
    $( Continuity of the second projection map of a topological product.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    tx2cn $p |- ( ( R e. Top /\ S e. Top ) ->
                  ( 2nd |` Z ) e. ( ( R tX S ) Cn S ) ) $=
      ( vz vu vv ctop wcel wa c2nd cxp wceq wss cvv wi vw cres ctx co cuni ccnv
      ccn wf cv cima wral wb txtop eqid iscn sylancom f2ndres a1i reseq2d feq1d
      txuni feq2d bitr4d mpbiri wrex cab ctg cfv ctb txbas bastg txval sseqtr4d
      syl adantr topopn ad2antrr simpr reseq2i cnveqi imaeq1i crn imassrn sseli
      cdm cin dfdm4 dmres eqtr3i wfo fo2nd fof mp2b ineq2i inv1 3eqtri syl6eleq
      fdm wfun ffun ax-mp fvimacnvi mpan fvres eleq1d biimpd c1st xp1st biimpri
      xpss elxp7 syl12anc ex syldd com23 syl5 mpdi ssrdv eltopss xpss2 adantlll
      sseld imp xp2nd adantl eqeltrd ralrimiva adantll fdmi syl6sseqr funimass4
      sylancr mpbird funimass3 mpbid eqssd syl5eq eqeq2d uniexg syl5eqel syl2an
      xpeq1 xpeq2 rcla42ev syl3anc xpexg fex cnvexg imaexg eqeq1 2rexbidv elabg
      3syl sseldd mpbir2and ) ALMZBLMZNZOEUBZABUCUDZBUGUDMZUUTUEZDUUSUHZUUSUFZU
      AUIZUJZUUTMZUABUKZUUPUUQUUTLMUVAUVCUVHNULABUMUAUUSUUTBUVBDUVBUNGUOUPUURUV
      CCDPZDOUVIUBZUHZCDUQZUURUVCUVBDUVJUHUVKUURUVBDUUSUVJUUREUVIOEUVIQUURHURUS
      UTUURUVIUVBDUVJABCDFGVAVBVCVDUURUVGUABUURUVEBMZNZIUIZJUIZKUIZPZQZKBVEJAVE
      ZIVFZUUTUVFUURUWAUUTRUVMUURUWAUWAVGVHZUUTUURUWAVIMUWAUWBRIKJABVJUWAVKVNIK
      JABVLVMVOUVNUVFUWAMZUVFUVRQZKBVEJAVEZUVNCAMZUVMUVFCUVEPZQZUWEUUPUWFUUQUVM
      ACFVPVQUURUVMVRUVNUVFUVJUFZUVEUJZUWGUVDUWIUVEUUSUVJEUVIOHVSZVTWAUVNUWJUWG
      UVNIUWJUWGUVNUVOUWJMZUVOUVIMZUVOUWGMZUWLUVOUWIWBZUVIUWJUWOUVOUWIUVEWCWDUW
      OUVIOWEZWFZUVISWFUVIUVJWEZUWOUWQUVJWGOUVIWHWIUWPSUVISSOWJSSOUHUWPSQWKSSOW
      LSSOWRWMWNUVIWOWPWQUWLUVOUVJVHZUVEMZUVNUWMUWNTUVJWSZUWLUWTUVKUXAUVLUVIDUV
      JWTXAZUVOUVEUVJXBXCUVNUWMUWTUWNUVNUWMUWTUVOOVHZUVEMZUWNUWMUWTUXDTTUVNUWMU
      WTUXDUWMUWSUXCUVEUVOUVIOXDZXEXFURUWMUXDUWNTTUVNUWMUXDUWNUWMUXDNUVOSSPZMZU
      VOXGVHCMZUXDUWNUWMUXGUXDUVIUXFUVOCDXJWDVOUWMUXHUXDUVOCDXHVOUWMUXDVRUWNUXG
      UXHUXDNNUVOCUVEXKXIXLXMURXNXOXPXQXRUVNUVJUWGUJUVERZUWGUWJRZUVNUXIUWTIUWGU
      KZUVNUWTIUWGUVNUWNNZUWSUXCUVEUXLUWMUWSUXCQUUQUVMUWNUWMUUPUUQUVMNZUWNUWMUX
      MUWGUVIUVOUXMUVEDRUWGUVIRZUVEBDGXSUVEDCXTVNZYBYCYAUXEVNUWNUXDUVNUVOCUVEYD
      YEYFYGUVNUXAUWGUWRRZUXIUXKULUXBUVNUWGUVIUWRUUQUVMUXNUUPUXOYHUVIDUVJUVLYIY
      JZIUWGUVEUVJYKYLYMUVNUXAUXPUXIUXJULUXBUXQUWGUVEUVJYNYLYOYPYQUWDUWHUVFCUVQ
      PZQJKCUVEABUVPCQUVRUXRUVFUVPCUVQUUBYRUVQUVEQUXRUWGUVFUVQUVECUUCYRUUDUUEUV
      NUVFSMZUWCUWEULUVNUUSSMUVDSMUXSUVNUUSUVJSUWKUURUVJSMZUVMUURUVKUVISMZUXTUV
      LUUPCSMDSMUYAUUQUUPCAUESFALYSYTUUQDBUESGBLYSYTCDSSUUFUUAUVIDSUVJUUGYLVOYT
      UUSSUUHUVDUVESUUIUUMUVTUWEIUVFSUVOUVFQUVSUWDJKABUVOUVFUVRUUJUUKUULVNYMUUN
      YGUUO $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d A h x y z $.  $d B h x y z $.  $d C h x y z $.  $d D h x y z $.
    $d F h x y z $.  $d G h x y z $.  $d P h x y z $.  $d Q h x y z $.
    upxp.1 $e |- P = ( 1st |` ( B X. C ) ) $.
    upxp.2 $e |- Q = ( 2nd |` ( B X. C ) ) $.
    $( Universal property of the Cartesian product considered as a categorical
       product in the category of sets.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    upxp $p |- ( ( A e. D /\ F : A --> B /\ G : A --> C ) ->
        E! h ( h : A --> ( B X. C ) /\ F = ( P o. h ) /\ G = ( Q o. h ) ) ) $=
      ( vz wcel wceq cfv wa cvv wfn syl c1st vx vy wf w3a cxp cv ccom weu copab
      cop opabex2g eueq sylib 3ad2ant1 wral ffn adantl ffvelrn opelxpi anandirs
      wb syl2an ralrimiva 3adant1 eqid fopab2 adantr eqfnfv syl2anc eqidd sseli
      c2nd xpss 3ad2antl1 adantll cres fveq1 coeq1i fveq1i syl6eq 3ad2ant2 wfun
      ad2antlr cdm wss wfo fo1st fofn ax-mp ssv fnssres mp2an fnfun ffun eleq2d
      a1i fdm biimpar syl3anc fvres 3eqtrrd 3ad2ant3 fo2nd eqopi syl12anc fveq2
      fvco opeq12d opex fvopab4 eqtr4d mpbir2and ex crn frn fnco 3adantl1 op1st
      fveq2d fvex 3eqtrd eqtr2d syl6eqr op2nd 3jca feq1 coeq2 eqeq2d syl5ibrcom
      3anbi123d impbid eubidv mpbird ) ADMZABHUCZACIUCZUDZABCUEZGUFZUCZHEYSUGZN
      ZIFYSUGZNZUDZGUHYSUAUFZAMZUBUFUUFHOZUUFIOZUJZNPUAUBUIZNZGUHZYNYOUUMYPYNUU
      KQMUUMUAUBAUUJDUKGUUKULUMUNYQUUEUULGYQUUEUULYQUUEUULYQUUEPZUULAANZLUFZYSO
      ZUUPUUKOZNZLAUOZUUNYSARZUUKARZUULUUOUUTPVAUUEUVAYQYTUUBUVAUUDAYRYSUPUNUQY
      QUVBUUEYQAYRUUKUCZUVBYQUUJYRMZUAAUOZUVCYOYPUVEYNYOYPPUVDUAAYOYPUUGUVDYOUU
      GPUUHBMUUICMUVDYPUUGPABUUFHURACUUFIURUUHUUIBCUSVBUTVCVDUAUBAYRUUJUUKUUKVE
      ZVFUMZAYRUUKUPSZVGLAAYSUUKVHVIUUNAVJUUNUUSLAUUNUUPAMZPZUUQUUPHOZUUPIOZUJZ
      UURUVJUUQQQUEZMZUUQTOZUVKNUUQVLOZUVLNUUQUVMNUUEUVIUVOYQYTUUBUVIUVOUUDYTUV
      IPUUQYRMZUVOAYRUUPYSURZYRUVNUUQBCVMVKSVNVOUVJUVKUUPTYRVPZYSUGZOZUUQUVTOZU
      VPUUEUVKUWBNZYQUVIUUBYTUWDUUDUUBUVKUUPUUAOUWBUUPHUUAVQUUPUUAUWAEUVTYSJVRV
      SVTWAWCUVJUVTWBZYSWBZUUPYSWDZMZUWBUWCNUWEUVJUVTYRRZUWETQRZYRQWEZUWIQQTWFU
      WJWGQQTWHWIYRWJZQYRTWKWLZYRUVTWMWIZWPUUEUWFYQUVIYTUUBUWFUUDAYRYSWNUNWCZUU
      EUVIUWHYQYTUUBUVIUWHUUDYTUWHUVIYTUWGAUUPAYRYSWQWOWRVNVOZUUPUVTYSXGWSUVJUV
      RUWCUVPNUUEUVIUVRYQYTUUBUVIUVRUUDUVSVNVOZUUQYRTWTSXAUVJUVLUUPVLYRVPZYSUGZ
      OZUUQUWROZUVQUUEUVLUWTNZYQUVIUUDYTUXBUUBUUDUVLUUPUUCOUWTUUPIUUCVQUUPUUCUW
      SFUWRYSKVRVSVTXBWCUVJUWRWBZUWFUWHUWTUXANUXCUVJUWRYRRZUXCVLQRZUWKUXDQQVLWF
      UXEXCQQVLWHWIUWLQYRVLWKWLZYRUWRWMWIZWPUWOUWPUUPUWRYSXGWSUVJUVRUXAUVQNUWQU
      UQYRVLWTSXAUUQUVKUVLQQXDXEUVIUURUVMNZUUNUAUBUUPUUJUVMAUUKUUFUUPNUUHUVKUUI
      UVLUUFUUPHXFUUFUUPIXFXHUVFUVKUVLXIXJZUQXKVCXLXMYQUUEUULUVCHEUUKUGZNZIFUUK
      UGZNZUDYQUVCUXKUXMUVGYQHUVTUUKUGZUXJYQHUXNNZUUOUVKUUPUXNOZNZLAUOZYQHARZUX
      NARZUXOUUOUXRPVAYOYNUXSYPABHUPWAYQUWIUVBUUKXNYRWEZUXTUWIYQUWMWPUVHYQUVCUY
      AUVGAYRUUKXOSZYRAUVTUUKXPWSLAAHUXNVHVIYQAVJZYQUXQLAYQUVIPZUXPUURUVTOZUVKU
      YDUWEUUKWBZUUPUUKWDZMZUXPUYENUWEUYDUWNWPUYDUVCUYFYQUVCUVIUVGVGAYRUUKWNSZY
      QUYHUVIYQUYGAUUPYQUVCUYGANUVGAYRUUKWQSWOWRZUUPUVTUUKXGWSUYDUYEUVMUVTOZUVM
      TOZUVKUYDUURUVMUVTUVIUXHYQUXIUQZXSUYDUVMYRMZUYKUYLNYOYPUVIUYNYNYOYPUVIUYN
      YOUVIPUVKBMUVLCMUYNYPUVIPABUUPHURACUUPIURUVKUVLBCUSVBUTXQZUVMYRTWTSUYLUVK
      NUYDUVKUVLUUPHXTZXRWPYAYBVCXLEUVTUUKJVRYCYQIUWRUUKUGZUXLYQIUYQNZUUOUVLUUP
      UYQOZNZLAUOZYQIARZUYQARZUYRUUOVUAPVAYPYNVUBYOACIUPXBYQUXDUVBUYAVUCUXDYQUX
      FWPUVHUYBYRAUWRUUKXPWSLAAIUYQVHVIUYCYQUYTLAUYDUYSUURUWROZUVLUYDUXCUYFUYHU
      YSVUDNUXCUYDUXGWPUYIUYJUUPUWRUUKXGWSUYDVUDUVMUWROZUVMVLOZUVLUYDUURUVMUWRU
      YMXSUYDUYNVUEVUFNUYOUVMYRVLWTSVUFUVLNUYDUVKUVLUYPUUPIXTYDWPYAYBVCXLFUWRUU
      KKVRYCYEUULYTUVCUUBUXKUUDUXMAYRYSUUKYFUULUUAUXJHYSUUKEYGYHUULUUCUXLIYSUUK
      FYGYHYJYIYKYLYM $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d R h s t u v w x y z $.  $d S h s t u v w x y z $.
    $d X h s t u v w x y z $.  $d T h s t u v w x y z $.
    $d U h s t u v w x y z $.  $d Y h s t u v w x y z $.
    $d P h s t u v w x y z $.  $d Q h s t u v w x y z $.
    $d Z h s t u v w x y z $.  $d F h s t u v w x y z $.
    $d G h s t u v w x y z $.
    uptx.1 $e |- T = ( R tX S ) $.
    uptx.2 $e |- X = U. R $.
    uptx.3 $e |- Y = U. S $.
    uptx.4 $e |- Z = ( X X. Y ) $.
    uptx.5 $e |- P = ( 1st |` Z ) $.
    uptx.6 $e |- Q = ( 2nd |` Z ) $.
    $( Universal property of the binary topological product.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    uptx $p |- ( ( ( R e. Top /\ S e. Top /\ U e. Top ) /\
                          ( F e. ( U Cn R ) /\ G e. ( U Cn S ) ) ) ->
                  E! h e. ( U Cn T ) ( F = ( P o. h ) /\ G = ( Q o. h ) ) ) $=
      ( wcel wa vx vy vz vs vw vu vv vt ctop w3a ccn co ccom wceq wex wreu cuni
      cv wmo cfv cop copab cvv uniexg opabex2g syl 3ad2ant3 adantr wf ccnv cima
      wral ctx syl5eqel eqid sylan2 ancoms 3impa cxp cnf 3expia ffvelrn opelxpi
      wb wi imp syl2an anandirs ralrimiva sylib 3adant3 feq3 wss eleq2d simpll3
      wrex fveq2 eleq1d fvex syl6bb adantl wfn ffn elpreima sylan cnima ancom1s
      3expa syl3anc imaeq2 syl2anc mpbir2and c1st cres fofn ax-mp fnssres mp2an
      ex wfo a1i fnco eqfnfv wfun fnfun fvco fveq2d fvres 3eqtrd eqtr2d reseq2i
      eqtri coeq1i syl6eqr c2nd coeq2 eqeq2d anbi12d sylc weu txtop iscn fopab2
      3adant2 3adant1 anim12d txuni unieqi syl6reqr mpbird cab ctg txval syl5eq
      ctb txbas eltg3 bitrd ciun ssel2 vex eqeq1 2rexbidv elab cin opeq12d opex
      imauni fvopab4 opelxp pm5.32da anandi 3syl elin bi2anan9 3bitr4d 3adantl2
      syl5bb eqrdv adantlrr adantrr 3adantl1 adantlrl adantrl eqeltrd rexlimdvv
      inopn biimprcd syl6 anassrs iunopn syl5ibrcom expimpd sylbid ralrimiv crn
      exlimdv fo1st ssv frn eqidd cdm fdm biimpar op1st fo2nd op2nd jca32 eleq1
      ffun cla4egv imdistani wal sylibd anim1d 3anass syl6ibr alrimiv upxp eumo
      3expb 3ad2antl3 immo df-reu eu5 bitri sylanbrc ) CUISZDUISZFUISZUJZHFCUKU
      LSZIFDUKULSZTZTZGURZFEUKULZSZHAUYPUMZUNZIBUYPUMZUNZTZTZGUOZVUDGUSZVUCGUYQ
      UPZUYOUAURZFUQZSZUBURVUHHUTZVUHIUTZVAZUNTUAUBVBZVCSZVUNUYQSZHAVUNUMZUNZIB
      VUNUMZUNZTZTZVUEUYKVUOUYNUYJUYHVUOUYIUYJVUIVCSZVUOFUIVDZUAUBVUIVUMVCVEVFV
      GVHUYOVUPVURVUTUYOVUPVUIEUQZVUNVIZVUNVJZUCURZVKZFSZUCEVLZUYKVUPVVFVVKTWDZ
      UYNUYHUYIUYJVVLUYJUYHUYITZVVLVVMUYJEUISZVVLVVMECDVMULZUIMCDUUAVNZUCVUNFEV
      UIVVEVUIVOZVVEVOZUUBVPVQVRVHUYOVVFVUIJKVSZVUNVIZUYOVUIJHVIZVUIKIVIZTZVVTU
      YKUYNVWCUYKUYLVWAUYMVWBUYHUYJUYLVWAWEZUYIUYJUYHVWDUYJUYHUYLVWAHFCVUIJVVQN
      VTWAVQUUDUYIUYJUYMVWBWEZUYHUYJUYIVWEUYJUYIUYMVWBIFDVUIKVVQOVTWAVQUUEUUFZW
      FZVWCVUMVVSSZUAVUIVLVVTVWCVWHUAVUIVWAVWBVUJVWHVWAVUJTVUKJSVULKSVWHVWBVUJT
      VUIJVUHHWBVUIKVUHIWBVUKVULJKWCWGWHWIUAUBVUIVVSVUMVUNVUNVOZUUCWJZVFUYOVVEV
      VSUNZVVFVVTWDUYKVWKUYNUYHUYIVWKUYJVVMVVSVVOUQVVECDJKNOUUGEVVOMUUHUUIWKZVH
      VVEVVSVUIVUNWLVFUUJUYOVVJUCEUYOVVHESZUDURZUEURZUFURZUGURZVSZUNZUGDWPUFCWP
      ZUEUUKZWMZVVHVWNUQZUNZTZUDUOZVVJUYKVWMVXFWDZUYNUYHUYIVXGUYJVVMVWMVVHVXAUU
      LUTZSZVXFVVMEVXHVVHVVMEVVOVXHMUEUGUFCDUUMUUNWNVVMVXAUUOSVXIVXFWDUEUGUFCDU
      UPUDVVHVXAUUQVFUURWKVHUYOVXEVVJUDUYOVXBVXDVVJUYOVXBTZVVJVXDVVGVXCVKZFSVXJ
      VXKUHVWNVVGUHURZVKZUUSZFUHVVGVWNUVHVXJUYJVXMFSZUHVWNVLVXNFSUYHUYIUYJUYNVX
      BWOVXJVXOUHVWNUYOVXBVXLVWNSZVXOVXBVXPTZUYOVXLVWRUNZUGDWPUFCWPZVXOVXQVXLVX
      ASVXSVWNVXAVXLUUTVWTVXSUEVXLUHUVAVWOVXLUNVWSVXRUFUGCDVWOVXLVWRUVBUVCUVDWJ
      UYOVXSVXOUYOVXRVXOUFUGCDUYOVWPCSZVWQDSZTZVVGVWRVKZFSZVXRVXOWEUYOVYBVYDUYO
      VYBTZVYCHVJVWPVKZIVJVWQVKZUVEZFUYOVWCVYBVYCVYHUNVWGVWCVYBTZUCVYCVYHVYIVVH
      VUISZVVHVUNUTZVWRSZTZVYJVVHHUTZVWPSZTZVYJVVHIUTZVWQSZTZTZVVHVYCSZVVHVYHSZ
      VYIVYMVYJVYOVYRTZTVYTVYIVYJVYLWUCVYJVYLWUCWDVYIVYJVYLVYNVYQVAZVWRSWUCVYJV
      YKWUDVWRUAUBVVHVUMWUDVUIVUNVUHVVHUNVUKVYNVULVYQVUHVVHHWQVUHVVHIWQUVFVWIVY
      NVYQUVGUVIZWRVYNVYQVWPVWQVVHIWSZUVJWTXAUVKVYJVYOVYRUVLWTVWCWUAVYMWDZVYBVW
      CVVTVUNVUIXBZWUGVWJVUIVVSVUNXCZVUIVVHVWRVUNXDUVMVHVWCWUBVYTWDVYBWUBVVHVYF
      SZVVHVYGSZTVWCVYTVVHVYFVYGUVNVWAWUJVYPVWBWUKVYSVWAHVUIXBZWUJVYPWDVUIJHXCZ
      VUIVVHVWPHXDVFVWBIVUIXBZWUKVYSWDVUIKIXCZVUIVVHVWQIXDVFUVOUVRVHUVPUVSXEVYE
      UYJVYFFSZVYGFSZVYHFSUYHUYIUYJUYNVYBWOUYOVXTWUPVYAUYKUYLVXTWUPUYMUYKUYLTVX
      TWUPUYHUYJUYLVXTWUPWEZUYIUYJUYHUYLWURUYJUYHUYLWURUYJUYHUYLUJVXTWUPVWPHFCX
      FXSXHXGUVQWFUVTUWAUYOVYAWUQVXTUYKUYMVYAWUQUYLUYKUYMTVYAWUQUYIUYJUYMVYAWUQ
      WEZUYHUYJUYIUYMWUSUYJUYIUYMWUSUYJUYIUYMUJVYAWUQVWQIFDXFXSXHXGUWBWFUWCUWDV
      YFVYGFUWGXIUWEXSVXRVXOVYDVXRVXMVYCFVXLVWRVVGXJWRUWHUWIUWFWFVPUWJWIUHVWNVX
      MFUWKXKVNVXDVVIVXKFVVHVXCVVGXJWRUWLUWMUWQUWNUWOXLUYOVWCVURVWGVWCHXMVVSXNZ
      VUNUMZVUQVWCHWVAUNZVUIVUIUNZVYNVVHWVAUTZUNZUCVUIVLZVWCWULWVAVUIXBZWVBWVCW
      VFTWDVWAWULVWBWUMVHVWCWUTVVSXBZWUHVUNUWPVVSWMZWVGWVHVWCXMVCXBZVVSVCWMZWVH
      VCVCXMXTWVJUWRVCVCXMXOXPVVSUWSZVCVVSXMXQXRZYAVWCVVTWUHVWJWUIVFZVWCVVTWVIV
      WJVUIVVSVUNUWTVFZVVSVUIWUTVUNYBXIUCVUIVUIHWVAYCXKVWCVUIUXAZVWCWVEUCVUIVWC
      VYJTZWVDVYKWUTUTZVYNWVQWUTYDZVUNYDZVVHVUNUXBZSZWVDWVRUNWVSWVQWVHWVSWVMVVS
      WUTYEXPYAWVQVVTWVTVWCVVTVYJVWJVHVUIVVSVUNUXJVFZVWCWWBVYJVWCWWAVUIVVHVWCVV
      TWWAVUIUNVWJVUIVVSVUNUXCVFWNUXDZVVHWUTVUNYFXIWVQWVRWUDWUTUTZWUDXMUTZVYNWV
      QVYKWUDWUTVYJVYKWUDUNVWCWUEXAZYGWVQWUDVVSSZWWEWWFUNVWAVWBVYJWWHVWAVYJTVYN
      JSVYQKSWWHVWBVYJTVUIJVVHHWBVUIKVVHIWBVYNVYQJKWCWGWHZWUDVVSXMYHVFWWFVYNUNW
      VQVYNVYQVVHHWSZUXEYAYIYJWIXLAWUTVUNAXMLXNWUTQLVVSXMPYKYLZYMYNVFUYOVWCVUTV
      WGVWCIYOVVSXNZVUNUMZVUSVWCIWWMUNZWVCVYQVVHWWMUTZUNZUCVUIVLZVWCWUNWWMVUIXB
      ZWWNWVCWWQTWDVWBWUNVWAWUOXAVWCWWLVVSXBZWUHWVIWWRWWSVWCYOVCXBZWVKWWSVCVCYO
      XTWWTUXFVCVCYOXOXPWVLVCVVSYOXQXRZYAWVNWVOVVSVUIWWLVUNYBXIUCVUIVUIIWWMYCXK
      WVPVWCWWPUCVUIWVQWWOVYKWWLUTZVYQWVQWWLYDZWVTWWBWWOWXBUNWXCWVQWWSWXCWXAVVS
      WWLYEXPYAWWCWWDVVHWWLVUNYFXIWVQWXBWUDWWLUTZWUDYOUTZVYQWVQVYKWUDWWLWWGYGWV
      QWWHWXDWXEUNWWIWUDVVSYOYHVFWXEVYQUNWVQVYNVYQWWJWUFUXGYAYIYJWIXLBWWLVUNBYO
      LXNWWLRLVVSYOPYKYLZYMYNVFUXHVUDVVBGVUNVCUYPVUNUNZUYRVUPVUCVVAUYPVUNUYQUXI
      WXGUYTVURVUBVUTWXGUYSVUQHUYPVUNAYPYQWXGVUAVUSIUYPVUNBYPYQYRYRUXKYSUYOUYKV
      WCTZVUFUYKUYNVWCVWFUXLWXHVUDVUIVVSUYPVIZUYTVUBUJZWEZGUXMWXJGUSZVUFWXHWXKG
      WXHVUDWXIVUCTWXJWXHUYRWXIVUCUYKUYRWXIWEVWCUYKUYRVUIVVEUYPVIZWXIUYHUYIUYJU
      YRWXMWEZUYJVVMWXNVVMUYJVVNWXNVVPUYJVVNUYRWXMUYPFEVUIVVEVVQVVRVTWAVPVQVRUY
      KVWKWXMWXIWDVWLVVEVVSVUIUYPWLVFUXNVHUXOWXIUYTVUBUXPUXQUXRUYJUYHVWCWXLUYIU
      YJVVCVWCWXLVVDVVCVWAVWBWXLVVCVWAVWBUJWXJGYTWXLVUIJKVCABGHIWWKWXFUXSWXJGUX
      TVFUYAXEUYBVUDWXJGUYCYSVFVUGVUDGYTVUEVUFTVUCGUYQUYDVUDGUYEUYFUYG $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d R h x y $.  $d S h x y $.  $d T h x y $.  $d U h x y $.  $d W h x y $.
    $d X h x y $.  $d Y h x y $.  $d Z h x y $.  $d P h x y $.  $d Q h x y $.
    $d F h x y $.
    txcn.1 $e |- X = U. R $.
    txcn.2 $e |- Y = U. S $.
    txcn.3 $e |- Z = ( X X. Y ) $.
    txcn.4 $e |- W = U. U $.
    txcn.5 $e |- P = ( 1st |` Z ) $.
    txcn.6 $e |- Q = ( 2nd |` Z ) $.
    $( A map into the product of two topological spaces is continuous iff both
       of its projections are continuous.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    txcn $p |- ( ( ( R e. Top /\ S e. Top /\ U e. Top ) /\ F : W --> Z ) ->
         ( F e. ( U Cn ( R tX S ) ) <-> ( ( P o. F ) e. ( U Cn R ) /\
                                          ( Q o. F ) e. ( U Cn S ) ) ) ) $=
      ( wcel wa wi wceq vh vx vy ctop w3a wf ctx co ccn ccom c1st cres syl5eqel
      tx1cn 3adant3 simp3 txtop simp1 cnco syl3anc mpan2d c2nd tx2cn simp2 jcad
      ex adantr cv wrex wreu eqid uptx reurex syl 3expia ancoms 3adant2 3adant1
      cnf anim12d cuni sylan2 3impa wb txuni feq3 sylibrd ad2antrr simpr simpll
      cxp simplr adantll ax-mp biimpi eqidd ad2antlr wal weu cvv uniexg reseq2i
      3jca eqtri upxp 3expb sylan 3ad2antl3 adantlr feq1 coeq2 eqeq2d 3anbi123d
      wex eu4 simprbi vex ad3antrrr anbi1d equequ1 imbi12d anbi2d eqeq2 cla42gv
      fex sylan9bb sylancr mpd mp2and exp31 eleq1 syl8 com24 mpdd rexlimdv syld
      biimpd impbid ) CUDQZDUDQZEUDQZUEZGJFUFZRZFECDUGUHZUIUHZQZAFUJZECUIUHQZBF
      UJZEDUIUHQZRZUUBUUGUULSUUCUUBUUGUUIUUKUUBUUGAUUECUIUHZQZUUIYSYTUUNUUAYSYT
      RZAUKJULZUUMOCDHIJKLMUNUMUOUUBUUAUUEUDQZYSUUGUUNRZUUISYSYTUUAUPZYSYTUUQUU
      ACDUQZUOZYSYTUUAURUUAUUQYSUEUURUUIFAEUUECUSVFUTVAUUBUUGBUUEDUIUHZQZUUKYSY
      TUVCUUAUUOBVBJULZUVBPCDHIJKLMVCUMUOUUBUUAUUQYTUUGUVCRZUUKSUUSUVAYSYTUUAVD
      UUAUUQYTUEUVEUUKFBEUUEDUSVFUTVAVEVGUUDUULUUHAUAVHZUJZTZUUJBUVFUJZTZRZUAUU
      FVIZUUGUUBUULUVLSUUCUUBUULUVLUUBUULRUVKUAUUFVJUVLABCDUUEEUAUUHUUJHIJUUEVK
      KLMOPVLUVKUAUUFVMVNVFVGUUDUULGHUUHUFZGIUUJUFZRZUVLUUGSZUUBUULUVOSUUCUUBUU
      IUVMUUKUVNYSUUAUUIUVMSZYTUUAYSUVQUUAYSUUIUVMUUHECGHNKVSVOVPVQYTUUAUUKUVNS
      ZYSUUAYTUVRUUAYTUUKUVNUUJEDGINLVSVOVPVRVTVGUUDUVOUVPUUDUVORZUVKUUGUAUUFUV
      SUVFUUFQZGHIWKZUVFUFZUVKUUGSUUBUVTUWBSUUCUVOUUBUVTGUUEWAZUVFUFZUWBYSYTUUA
      UVTUWDSZUUAUUOUWEUUOUUAUUQUWEUUTUUAUUQUVTUWDUVFEUUEGUWCNUWCVKVSVOWBVPWCUU
      BUWAUWCTZUWBUWDWDYSYTUWFUUACDHIKLWEUOUWAUWCGUVFWFVNWGWHUVSUVKUWBUVTUUGUVS
      UVKUWBUVFFTZUVTUUGSUVSUVKUWBUWGUVSUVKRUWBRZUWBUVHUVJUEZGUWAFUFZUUHUUHTZUU
      JUUJTZUEZUWGUVKUWBUWIUVSUVKUWBRUWBUVHUVJUVKUWBWIUVHUVJUWBWJUVHUVJUWBWLXCW
      MUVSUWMUVKUWBUUCUWMUUBUVOUUCUWJUWKUWLUUCUWJJUWATUUCUWJWDMJUWAGFWFWNWOUUCU
      UHWPUUCUUJWPXCWQWHUWHGUWAUBVHZUFZUUHAUWNUJZTZUUJBUWNUJZTZUEZGUWAUCVHZUFZU
      UHAUXAUJZTZUUJBUXAUJZTZUEZRZUWNUXATZSZUCWRUBWRZUWIUWMRZUWGSZUWHUWTUBWSZUX
      KUVSUXNUVKUWBUUBUVOUXNUUCUUAYSUVOUXNYTUUAGWTQZUVOUXNUUAGEWAWTNEUDXAUMZUXO
      UVMUVNUXNGHIWTABUBUUHUUJAUUPUKUWAULOJUWAUKMXBXDBUVDVBUWAULPJUWAVBMXBXDXEX
      FXGXHXIWHUXNUWTUBXNUXKUWTUXGUBUCUXIUWOUXBUWQUXDUWSUXFGUWAUWNUXAXJUXIUWPUX
      CUUHUWNUXAAXKXLUXIUWRUXEUUJUWNUXABXKXLXMXOXPVNUWHUVFWTQFWTQZUXKUXMSUAXQUU
      DUXQUVOUVKUWBUUAYSUUCUXQYTUUCUUAUXQUUAUUCUXOUXQUXPGJWTFYEWBVPXHXRUXJUXMUB
      UCUVFFWTWTUWNUVFTZUXJUWIUXGRZUVFUXATZSUXAFTZUXMUXRUXHUXSUXIUXTUXRUWTUWIUX
      GUXRUWOUWBUWQUVHUWSUVJGUWAUWNUVFXJUXRUWPUVGUUHUWNUVFAXKXLUXRUWRUVIUUJUWNU
      VFBXKXLXMXSUBUAUCXTYAUYAUXSUXLUXTUWGUYAUXGUWMUWIUYAUXBUWJUXDUWKUXFUWLGUWA
      UXAFXJUYAUXCUUHUUHUXAFAXKXLUYAUXEUUJUUJUXAFBXKXLXMYBUXAFUVFYCYAYFYDYGYHYI
      YJUWGUVTUUGUVFFUUFYKYQYLYMYNYOVFYPYNYR $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x z F $.  $d x z G $.  $d z H $.  $d x z R $.  $d x z S $.  $d x z W $.
    txcnmpt.1 $e |- W = U. U $.
    txcnmpt.2 $e |- H = ( x e. W |-> <. ( F ` x ) , ( G ` x ) >. ) $.
    $( A map into the product of two topological spaces is continuous if both
       of its projections are continuous.  (Contributed by Jeff Madsen,
       2-Sep-2009.)  (Revised by Mario Carneiro, 4-May-2014.) $)
    txcnmpt $p |- ( ( ( R e. Top /\ S e. Top /\ U e. Top ) /\
                ( F e. ( U Cn R ) /\ G e. ( U Cn S ) ) )
                 -> H e. ( U Cn ( R tX S ) ) ) $=
      ( vz wcel wa c1st c2nd wceq cfv wfn cvv syl ctop w3a ccn co ctx cuni cres
      cxp ccom wf wi eqid cnf 3expia ancoms 3adant2 3adant1 anim12d imp cv wral
      wb crn wss wfo fo1st fofn ax-mp ssv fnssres mp2an a1i cop ffvelrn opelxpi
      syl2an anandirs fmptd ffn frn fnco syl3anc adantr eqfnfv syl2anc wfun cdm
      eqidd fnfun ffun fdm eleq2d biimpar fvco fveq2 opeq12d opex adantl fveq2d
      fvmpt fvres fvex op1st syl6eq 3eqtrd ralrimiva eleq1d fo2nd op2nd anbi12d
      mpbir2and biimprcd mpd txcn syldan mpbird ) BUALZCUALZDUALZUBZEDBUCUDZLZF
      DCUCUDZLZMZMZGDBCUEUDUCUDLZNBUFZCUFZUHZUGZGUIZYALZOYJUGZGUIZYCLZMZYFHYHEU
      JZHYIFUJZMZYQXTYEYTXTYBYRYDYSXQXSYBYRUKZXRXSXQUUAXSXQYBYREDBHYHIYHULZUMUN
      UOUPXRXSYDYSUKZXQXSXRUUCXSXRYDYSFDCHYIIYIULZUMUNUOUQURUSZYEYTYQUKXTYTYQYE
      YTYMYBYPYDYTYLEYAYTYLEPZHHPZKUTZYLQZUUHEQZPZKHVAZYTYLHRZEHRZUUFUUGUULMVBY
      TYKYJRZGHRZGVCYJVDZUUMUUOYTNSRZYJSVDZUUOSSNVEUURVFSSNVGVHYJVIZSYJNVJVKZVL
      YTHYJGUJZUUPYTAHAUTZEQZUVCFQZVMZYJGYRYSUVCHLZUVFYJLZYRUVGMUVDYHLUVEYILUVH
      YSUVGMHYHUVCEVNHYIUVCFVNUVDUVEYHYIVOVPVQJVRZHYJGVSTZYTUVBUUQUVIHYJGVTTZYJ
      HYKGWAWBYRUUNYSHYHEVSWCKHHYLEWDWEYTHWHZYTUUKKHYTUUHHLZMZUUIUUHGQZYKQZUUJU
      UHFQZVMZYKQZUUJUVNYKWFZGWFZUUHGWGZLZUUIUVPPUVTUVNUUOUVTUVAYJYKWIVHVLUVNUV
      BUWAYTUVBUVMUVIWCHYJGWJTZYTUWCUVMYTUWBHUUHYTUVBUWBHPUVIHYJGWKTWLWMZUUHYKG
      WNWBUVNUVOUVRYKUVMUVOUVRPYTAUUHUVFUVRHGUVCUUHPUVDUUJUVEUVQUVCUUHEWOUVCUUH
      FWOWPJUUJUVQWQWTWRZWSUVNUVSUVRNQZUUJUVNUVRYJLZUVSUWGPYRYSUVMUWHYRUVMMUUJY
      HLUVQYILUWHYSUVMMHYHUUHEVNHYIUUHFVNUUJUVQYHYIVOVPVQZUVRYJNXATUUJUVQUUHEXB
      ZXCXDXEXFXKXGYTYOFYCYTYOFPZUUGUUHYOQZUVQPZKHVAZYTYOHRZFHRZUWKUUGUWNMVBYTY
      NYJRZUUPUUQUWOUWQYTOSRZUUSUWQSSOVEUWRXHSSOVGVHUUTSYJOVJVKZVLUVJUVKYJHYNGW
      AWBYSUWPYRHYIFVSWRKHHYOFWDWEUVLYTUWMKHUVNUWLUVOYNQZUVRYNQZUVQUVNYNWFZUWAU
      WCUWLUWTPUXBUVNUWQUXBUWSYJYNWIVHVLUWDUWEUUHYNGWNWBUVNUVOUVRYNUWFWSUVNUXAU
      VROQZUVQUVNUWHUXAUXCPUWIUVRYJOXATUUJUVQUWJUUHFXBXIXDXEXFXKXGXJXLWRXMXTYEU
      VBYGYQVBYFYTUVBUUEUVITYKYNBCDGHYHYIYJUUBUUDYJULIYKULYNULXNXOXP $.
      $( [4-May-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d R r s u v w x y z $.  $d S r s u v w x y z $.  $d V r s u v w x y z $.
    $d W r s u v w x y z $.  $d A r s u v w x y z $.  $d B r s u v w x y z $.
    $( The subspace of a topological product space induced by a subset with a
       Cartesian product representation is a topological product of the
       subspaces induced by the subspaces of the terms of the products.
       (Contributed by Jeff Madsen, 2-Sep-2009.)  (The proof was shortened by
       Mario Carneiro, 15-Dec-2013.) $)
    txsubsp $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. V /\ B e. W ) ) ->
                      ( subSp ` <. ( A X. B ) , ( R tX S ) >. ) =
                    ( ( subSp ` <. A , R >. ) tX ( subSp ` <. B , S >. ) ) ) $=
      ( vx vz vu vv vy vr vs ctop wcel wa cv wceq wrex syl5ibrcom vw cxp ctx co
      cop csubsp cfv cab wss cuni wex cin cvv wb txtop xpexg elsubsp eqid eltpt
      syl2an adantr ssel2 vex eqeq1 2rexbidv elab sylib w3a an6 elsubspr eqeq2d
      wi xpeq1 xpeq2 rcla42ev mp3an3 sylbir inxp syl6eq eqeq1d 3expia rexlimdvv
      ineq1 sylan2 anassrs rexlimdva ss2abdv inuni abrexex sseq1 anbi12d cla4ev
      unieq sylancl anbi2d exbidv expimpd exlimdv sylbid stoig3 bi2anan9 reeanv
      imp syl6bbr ad2antrr ad2antlr txopn adantlr syl3anc xpeq12 syl6eqr eleq1d
      an4s ex eleq1a syl6 abssdv sstr ancoms sylan uniopn syl2anc impbid bitr4d
      eleq1 eqrdv ) CNOZDNOZPZAEOZBFOZPZPZGABUBZCDUCUDZUEUFUGZACUEUFUGZBDUEUFUG
      ZUCUDZYMGQZYPOZHQZYTIQZJQZUBZRZJYRSIYQSZGUHZUIZYTUUBUJZRZPZHUKZYTYSOZYMUU
      AUUMYMUUAYTKQZYNULZRZKYOSZUUMYIYONOZYNUMOZUUAUURUNYLCDUOZABEFUPZKYTYNYOUM
      UQUTYMUUQUUMKYOYMUUOYOOZPUUMUUQUUIUUPUUJRZPZHUKZYMUVCUVFYMUVCUAQZUUBLQZMQ
      ZUBZRZMDSLCSZHUHZUIZUUOUVGUJZRZPZUAUKZUVFYIUVCUVRUNYLUAHMLUUOUVMCDUVMURUS
      VAYMUVQUVFUAYMUVNUVPUVFYMUVNPZUVFUVPUUIUVOYNULZUUJRZPZHUKZUVSUUQKUVGSZGUH
      ZUUHUIZUVTUWEUJZRZUWCUVSUWDUUGGUVSUUQUUGKUVGUVSUUOUVGOZPUUGUUQUUPUUERZJYR
      SIYQSZYMUVNUWIUWKUVNUWIPZYMUUOUVJRZMDSLCSZUWKUWLUUOUVMOUWNUVGUVMUUOVBUVLU
      WNHUUOKVCUUBUUORUVKUWMLMCDUUBUUOUVJVDVEVFVGYMUWNUWKYMUWMUWKLMCDYIYLUVHCOZ
      UVIDOZPZUWMUWKVLYIYLUWQVHZUWKUWMUVHAULZUVIBULZUBZUUERZJYRSIYQSZUWRYGYJUWO
      VHZYHYKUWPVHZPUXCYGYJUWOYHYKUWPVIUXDUWSYQOZUWTYROZUXCUXEUVHACEVJUVIBDFVJU
      XFUXGUXAUXARZUXCUXAURUXBUXHUXAUWSUUDUBZRIJUWSUWTYQYRUUCUWSRZUUEUXIUXAUUCU
      WSUUDVMVKUUDUWTRZUXIUXAUXAUUDUWTUWSVNVKVOVPUTVQUWMUWJUXBIJYQYRUWMUUPUXAUU
      EUWMUUPUVJYNULZUXAUUOUVJYNWCUVHUVIABVRZVSVTVETWAWBXCWDWEUUQUUFUWJIJYQYRYT
      UUPUUEVDVETWFWGGKUVGYNWHUWBUWFUWHPHUWEKGUVGUUPUAVCWIUUBUWERZUUIUWFUWAUWHU
      UBUWEUUHWJUXNUUJUWGUVTUUBUWEWMVKWKWLWNUVPUVEUWBHUVPUVDUWAUUIUVPUUPUVTUUJU
      UOUVOYNWCVTWOWPTWQWRWSXCUUQUULUVEHUUQUUKUVDUUIYTUUPUUJVDWOWPTWFWSYMUULUUA
      HYMUUIUUKUUAYMUUIPZUUAUUKUUJYPOZUXOYPNOZUUBYPUIZUXPYMUXQUUIYIUUSUUTUXQYLU
      VAUVBYNYOUMWTUTVAYMUUHYPUIZUUIUXRYMUUGGYPYMUUFUUAIJYQYRYMUUCYQOZUUDYROZPZ
      UUEYPOZUUFUUAVLYMUYBUXJUXKPZMDSLCSZUYCYMUYBUXJLCSZUXKMDSZPZUYEYGYJYHYKUYB
      UYHUNYGYJPZUXTUYFYHYKPZUYAUYGLUUCACEUQMUUDBDFUQXAXMUXJUXKLMCDXBXDYMUYDUYC
      LMCDYMUWQUYDUYCVLYMUWQPZUYCUYDUXLYPOZUYKUUSUUTUVJYOOZUYLYIUUSYLUWQUVAXEYL
      UUTYIUWQUVBXFYIUWQUYMYLUVHUVICDXGXHUVJYNYOUMVJXIUYDUUEUXLYPUYDUUEUXAUXLUU
      CUWSUUDUWTXJUXMXKXLTXNWBWSUUEYPYTXOXPWBXQUUIUXSUXRUUBUUHYPXRXSXTUUBYPYAYB
      YTUUJYPYETWQWRYCYGYJYHYKUUNUUMUNZUYIYQNOYRNOUYNUYJACEWTBDFWTHGJIYTUUHYQYR
      UUHURUSUTXMYDYF $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}


