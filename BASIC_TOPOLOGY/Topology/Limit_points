$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Limit points
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c limPt $.

  $( Extend class notation with the limit point function for topologies. $)
  clp $a class limPt $.

  ${
    $d j x y $.
    $( Define a function on topologies whose value is the set of limit points
       of the subsets of the base set.  See ~ lpval . $)
    df-lp $a |- limPt = ( j e. Top |-> ( x e. ~P U. j |->
                  { y | y e. ( ( cls ` j ) ` ( x \ { y } ) ) } ) ) $.
  $}

  ${
    $d j n x y J $.  $d n x y S $.  $d n j x y X $.  $d n x y P $.
    lpfval.1 $e |- X = U. J $.
    $( The limit point function on the subsets of a topology's base set. $)
    lpfval $p |- ( J e. Top -> ( limPt ` J ) = ( x e. ~P X |->
                  { y | y e. ( ( cls ` J ) ` ( x \ { y } ) ) } ) ) $=
      ( vj ctop wcel cpw cv csn cdif ccl cfv cab cmpt cvv clp wceq cuni syl6eqr
      topopn pwexg mptexg 3syl unieq fveq2 fveq1d eleq2d abbidv mpteq12dv df-lp
      pweq syl fvmptg mpdan ) CGHZADIZBJZAJUSKLZCMNZNZHZBOZPZQHZCRNVESUQDCHURQH
      VFCDEUBDCUCAURVDQUDUEFCAFJZTZIZUSUTVGMNZNZHZBOZPVEGQRVGCSZAVIVMURVDVNVHDS
      VIURSVNVHCTDVGCUFEUAVHDUMUNVNVLVCBVNVKVBUSVNUTVJVAVGCMUGUHUIUJUKABFULUOUP
      $.
      $( [11-Nov-2013] $) $( [10-Feb-2007] $)

    $( The set of limit points of a subset of the base set of a topology.
       Alternate definition of limit point in [Munkres] p. 97. $)
    lpval $p |- ( ( J e. Top /\ S C_ X ) -> ( ( limPt ` J ) ` S ) =
                 { x | x e. ( ( cls ` J ) ` ( S \ { x } ) ) } ) $=
      ( vy ctop wcel wss wa clp cfv cpw cv cdif cab wceq adantr cvv syl2anc csn
      ccl cmpt lpfval fveq1d wb topopn elpw2g biimpar wi difss sstr mpan clsss3
      sseld sylan2 abssdv ssexg difeq1 fveq2d eleq2d abbidv eqid fvmptg eqtrd
      syl ) CGHZBDIZJZBCKLZLZBFDMZANZFNZVMUAZOZCUBLZLZHZAPZUCZLZVMBVOOZVQLZHZAP
      ZVGVKWBQVHVGBVJWAFACDEUDUERVIBVLHZWFSHZWBWFQVGWGVHVGDCHZWGVHUFCDEUGZBDCUH
      VFUIVIWFDIWIWHVIWEADVHVGWCDIZWEVMDHUJWCBIVHWKBVOUKWCBDULUMVGWKJWDDVMWCCDE
      UNUOUPUQVGWIVHWJRWFDCURTFBVTWFVLSWAVNBQZVSWEAWLVRWDVMWLVPWCVQVNBVOUSUTVAV
      BWAVCVDTVE $.
      $( [11-Nov-2013] $) $( [10-Feb-2007] $)

    $( The predicate " ` P ` is a limit point of ` S ` ." $)
    islp $p |- ( ( J e. Top /\ S C_ X ) -> ( P e. ( ( limPt ` J ) ` S ) <->
               P e. ( ( cls ` J ) ` ( S \ { P } ) ) ) ) $=
      ( vx ctop wcel wss wa clp cfv cv csn cdif ccl cab lpval eleq2d elex elab3
      wceq id sneq difeq2d fveq2d eleq12d syl6bb ) CGHBDIJZABCKLLZHAFMZBUKNZOZC
      PLZLZHZFQZHABANZOZUNLZHZUIUJUQAFBCDERSUPVAFAAUTTUKAUBZUKAUOUTVBUCVBUMUSUN
      VBULURBUKAUDUEUFUGUAUH $.
      $( [10-Feb-2007] $)

    $( The limits points of a subset are included in the subset's closure. $)
    lpsscls $p |- ( ( J e. Top /\ S C_ X ) -> ( ( limPt ` J ) ` S ) C_
               ( ( cls ` J ) ` S ) ) $=
      ( vx ctop wcel wss wa clp cfv csn cdif ccl cab lpval difss clsss mp3an3
      cv sseld abssdv eqsstrd ) BFGZACHZIZABJKKETZAUGLZMZBNKZKZGZEOAUJKZEABCDPU
      FULEUMUFUKUMUGUDUEUIAHUKUMHAUHQAUIBCDRSUAUBUC $.
      $( [26-Feb-2007] $)

    $( The limits points of a subset are included in the base set. $)
    lpss $p |- ( ( J e. Top /\ S C_ X ) -> ( ( limPt ` J ) ` S ) C_ X ) $=
      ( ctop wcel wss wa clp cfv ccl lpsscls clsss3 sstrd ) BEFACGHABIJJABKJJCA
      BCDLABCDMN $.
      $( [9-Nov-2007] $)

    $( The predicate " ` P ` is a limit point of ` S ` ," in terms of
       neighborhoods.  Definition of limit point in [Munkres] p. 97.  Although
       Munkres uses open neighborhoods, it also works for our more general
       neighborhoods. $)
    islp2 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
                ( P e. ( ( limPt ` J ) ` S ) <->
         A. n e. ( ( nei ` J ) ` { P } ) ( n i^i ( S \ { P } ) ) =/= (/) ) ) $=
      ( vx vy wcel wss cfv cdif wa wi wal c0 wral wb wceq imbi12d ctop w3a ccld
      clp csn cv crab cint cin wne cnei islp ssdifss clsval sylan2 eleq2d bitrd
      ccl 3adant3 elintrabg df-ral iscld imbi1d albidv syl5bb sylan9bbr 3adant2
      cpw wrex isneip elpwi dfss4 sylib eqcomd eleq1d adantl df-ne imbi2i con2b
      wn bitri incom eqeq1i reldisj syl adantr eldif baibr ralbidva eleq2 sseq1
      3adant1 anbi12d biantru syl6bbr rcla4ev anim2i anassrs imim1i alimi ax-17
      ssid hba1 eleq1 ineq1 neeq1d a4v eltopss ancoms simpl anim1i adantrr an4s
      jca adantlrr ssrin ssn0 ex ad2antlr embantd exp31 com4t imp rexlimdv a1dd
      imp4a alrimd impbid2 impexp vex imbi1i 3bitr4ri albii bitr2i difss elpw2g
      elpw cvv mpbiri difeq2 syl6bb cuni uniexg syl5eqel 3ad2ant1 eqeq2d syl2an
      sseq2 ralxfrd 3bitr4d bitr4i syl6rbb 3bitrd ) DUAIZBEJZAEIZUBZABDUDKKIZAB
      AUEZLZGUFZJZGDUCKZUGUHZIZUVAEJZEUVALZDIZMZUVBAUVAIZNZNZGOZCUFZUUTUIZPUJZC
      UUSDUKKKZQZUUNUUOUURUVERUUPUUNUUOMZUURAUUTDURKKZIUVEABDEFULUVSUVTUVDAUUOU
      UNUUTEJZUVTUVDSBEUUSUMZGUUTDEFUNUOUPUQUSUUNUUPUVEUVMRUUOUUPUVEUVKGUVCQZUU
      NUVMUVBGAUVCEUTUWCUVAUVCIZUVKNZGOUUNUVMUVKGUVCVAUUNUWEUVLGUUNUWDUVIUVKUVA
      DEFVBVCVDVEVFVGUUQUVRUVHUVKNZGEVHZQZUVMUUQUVRUVNEJZAHUFZIZUWJUVNJZMZHDVIZ
      MZUVPNZCOZUWHUUNUUPUVRUWQRUUOUVRUVNUVQIZUVPNZCOUUNUUPMZUWQUVPCUVQVAUWTUWS
      UWPCUWTUWRUWOUVPAHDUVNEFVJVCVDVEVGUUQUVNDIZAUVNIZUVPNZNZCUWGQZEEUVNLZLZDI
      ZUUTUXFJZAUXFIZNZNZCUWGQZUWQUWHUUOUUPUXEUXMRUUNUUOUUPMZUXDUXLCUWGUXNUVNUW
      GIZMUXAUXHUXCUXKUXOUXAUXHRUXNUXOUVNUXGDUXOUXGUVNUXOUWIUXGUVNSUVNEVKUVNEVL
      VMVNVOVPUXNUXCUXKRUXOUXCUVOPSZUXBVTZNZUXNUXKUXCUXBUXPVTZNUXRUVPUXSUXBUVOP
      VQVRUXBUXPVSWAUXNUXPUXIUXQUXJUUOUXPUXIRUUPUXPUUTUVNUIZPSZUUOUXIUVOUXTPUVN
      UUTWBWCUUOUWAUYAUXIRUWBUUTUVNEWDWEVEWFUUPUXQUXJRUUOUXJUUPUXQAEUVNWGWHVPTV
      EWFTWIWLUUQUWQUWIUXAMZUXBMZUVPNZCOZUXEUUQUWQUYEUWPUYDCUYCUWOUVPUWIUXAUXBU
      WOUXAUXBMUWNUWIUWMUXBHUVNDUWJUVNSZUWMUXBUVNUVNJZMUXBUYFUWKUXBUWLUYGUWJUVN
      AWJUWJUVNUVNWKWMUYGUXBUVNXBWNWOWPWQWRWSWTUUNUUPUYEUWQNUUOUWTUYEUWPCUWTCXA
      UYDCXCUWTUYEUWIUWNUVPUWTUYEUWNUVPNZUWIUWTUYEUYHUWTUYEMUWMUVPHDUYEUWTUWJEJ
      ZUWJDIZMZUWKMZUWJUUTUIZPUJZNZUYJUWMUVPNNZUYDUYOCHUVNUWJSZUYCUYLUVPUYNUYQU
      YBUYKUXBUWKUYQUWIUYIUXAUYJUVNUWJEWKUVNUWJDXDWMUVNUWJAWJWMUYQUVOUYMPUVNUWJ
      UUTXEXFTXGUWTUYOUYPUYJUWMUWTUYOUVPUYJUWMUWTUYOUVPNUYJUWMMUWTMUYLUYNUVPUYJ
      UWKUWTUYLUWLUYJUUNUWKUUPUYLUYJUUNMZUWKUYLUUPUYRUYKUWKUYRUYIUYJUUNUYJUYIUW
      JDEFXHXIUYJUUNXJXNXKXLXMXOUWMUYNUVPNZUYJUWTUWLUYSUWKUWLUYMUVOJZUYSUWJUVNU
      UTXPUYTUYNUVPUYMUVOXQXRWEVPXSXTYAYBYCUOYDXRYEYFYGVGYHUXEUXOUXDNZCOUYEUXDC
      UWGVAVUAUYDCUYBUXCNUWIUXDNUYDVUAUWIUXAUXCYIUYBUXBUVPYIUXOUWIUXDUVNECYJYQY
      KYLYMYNUUAUUQEYRIZUWHUXMRUUNUUOVUBUUPUUNEDUUBYRFDUAUUCUUDUUEVUBUWFUXLGCUX
      FUWGUWGVUBUXFUWGIZUXOVUBVUCUXFEJEUVNYOUXFEYRYPYSWFVUBUVGUWGIZUVAEUVGLZSZU
      VAUXFSZCUWGVIUVAUWGIZVUBVUDUVGEJEUVAYOUVGEYRYPYSVUHVUEUVAVUHUVFVUEUVASUVA
      EVKUVAEVLVMVNVUGVUFCUVGUWGUVNUVGSUXFVUEUVAUVNUVGEYTUUFWPUUGVUGUWFUXLRVUBV
      UGUVHUXHUVKUXKVUGUVGUXGDUVAUXFEYTVOVUGUVBUXIUVJUXJUVAUXFUUTUUHUVAUXFAWJTT
      VPUUIWEUUJUQUWHVUHUWFNZGOUVMUWFGUWGVAVUIUVLGVUIUVFUWFNUVLVUHUVFUWFUVAEGYJ
      YQYKUVFUVHUVKYIUUKYMWAUULUUM $.
      $( [26-Feb-2007] $)

    $( The closure of a subset of a topological space is the subset together
       with its limit points.  Theorem 6.6 of [Munkres] p. 97. $)
    clslp $p |- ( ( J e. Top /\ S C_ X ) -> ( ( cls ` J ) ` S ) =
                ( S u. ( ( limPt ` J ) ` S ) ) ) $=
      ( vx vn ctop wcel wss wa ccl cfv cv cin c0 wne wi wb sylibrd ex clp wo wn
      cun csn cdif cnei wral neindisj expr adantr ineq2d neeq1d adantl ralrimdv
      difsn simpll simplr clsss3 sseld imp islp2 syl3anc orrd elun sylibr ssrdv
      sscls lpsscls unssd eqssd ) BGHZACIZJZABKLLZAABUALLZUDZVNEVOVQVNEMZVOHZVR
      VQHZVNVSJZVRAHZVRVPHZUBVTWAWBWCWAWBUCZFMZAVRUEZUFZNZOPZFWFBUGLLZUHZWCWAWD
      WIFWJWAWDWEWJHZWIQWAWDJWLWEANZOPZWIWAWLWNQWDVNVSWLWNVRABWECDUIUJUKWDWIWNR
      WAWDWHWMOWDWGAWEVRAUPULUMUNSTUOWAVLVMVRCHZWCWKRVLVMVSUQVLVMVSURVNVSWOVNVO
      CVRABCDUSUTVAVRAFBCDVBVCSVDVRAVPVEVFTVGVNAVPVOABCDVHABCDVIVJVK $.
      $( [26-Feb-2007] $)

    $( A point belonging to a set's closure but not the set itself is a limit
       point. $)
    islpi $p |- ( ( ( J e. Top /\ S C_ X ) /\ ( P e. ( ( cls ` J ) ` S ) /\
       -. P e. S ) ) -> P e. ( ( limPt ` J ) ` S ) ) $=
      ( ctop wcel wss wa ccl cfv wn clp wi cun clslp eleq2d wo elun df-or bitri
      syl6bb biimpd imp32 ) CFGBDHIZABCJKKZGZABGZLZABCMKKZGZUEUGUIUKNZUEUGABUJO
      ZGZULUEUFUMABCDEPQUNUHUKRULABUJSUHUKTUAUBUCUD $.
      $( [8-Nov-2007] $)

    $( A subset of a topological space is closed iff it contains all its limit
       points.  Corollary 6.7 of [Munkres] p. 97. $)
    cldlp $p |- ( ( J e. Top /\ S C_ X ) -> ( S e. ( Clsd ` J ) <->
                 ( ( limPt ` J ) ` S ) C_ S ) ) $=
      ( ctop wcel wss wa ccld cfv ccl wceq clp iscld3 cun clslp ssequn2 syl6bbr
      eqeq1d bitrd ) BEFACGHZABIJFABKJJZALZABMJJZAGZABCDNUAUCAUDOZALUEUAUBUFAAB
      CDPSUDAQRT $.
      $( [16-Jun-2008] $) $( [26-Feb-2007] $)
  $}

