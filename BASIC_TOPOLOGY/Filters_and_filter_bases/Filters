$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Filters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Fil $. $( The class of all filters. $)

  $( Extend class notation with the class of all filters. $)
  cfil $a class Fil $.

  ${
    $d f x y $.
    $( The class of all filters.  Bourbaki TG I.36 def. 1 axioms FI, FIIa,
       FIIb, FIII. Filters are used to define the concept of limit in the
       general case.  It's a generalization of the idea of neighborhoods.
       Suppose you are in ` RR ` .  With neighborhoods you can express the idea
       of a variable that tends to a specific number but you can't express the
       idea of a variable that tends to infinity.  Filters relax the "axioms"
       of neighborhoods and then succeed in expressing the idea of something
       that tends to infinity.  Filters were invented by Cartan in 1937 and
       made famous by Bourbaki in his treatise.  A notion similar to the notion
       of filter is the concept of net invented by Moore and Smith in 1922.
       (Contributed by FL, 20-Jul-2007.) $)
    df-fil $a |- Fil = { f e. fBas |
      A. x e. ~P U. f ( ( f i^i ~P x ) =/= (/) -> x e. f ) } $.
  $}

  ${
    $d F f x $.  $d X f x $.
    isfil.1 $e |- X = U. F $.
    $( The predicate "is a filter."  (Contributed by FL, 20-Jul-2007.) $)
    isfil $p |- ( F e. Fil <-> ( F e. fBas /\
      A. x e. ~P X ( ( F i^i ~P x ) =/= (/) -> x e. F ) ) ) $=
      ( vf cv cpw cin c0 wne wcel cuni wral cfbas cfil wceq unieq syl6eqr pweqd
      wi ineq1 neeq1d eleq2 imbi12d raleqbidv df-fil elrab2 ) EFZAFZGZHZIJZUIUH
      KZTZAUHLZGZMBUJHZIJZUIBKZTZACGZMEBNOUHBPZUNUTAUPVAVBUOCVBUOBLCUHBQDRSVBUL
      URUMUSVBUKUQIUHBUJUAUBUHBUIUCUDUEAEUFUG $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d x F $.
    $( A filter is a filter base.  (Contributed by Jeff Hankins,
       2-Sep-2009.) $)
    filfbas $p |- ( F e. Fil -> F e. fBas ) $=
      ( vx cfil wcel cfbas cv cpw cin c0 wne wi cuni wral eqid isfil simplbi )
      ACDAEDABFZGHIJQADKBALZGMBARRNOP $.
      $( [27-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d F x y $.
    $( The empty set doesn't belong to a filter.  (Contributed by FL,
       20-Jul-2007.) $)
    filesn $p |- ( F e. Fil -> -. (/) e. F ) $=
      ( cfil wcel cfbas c0 wn filfbas 0nelfb syl ) ABCADCEACFAGAHI $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d B x $.  $d F x $.  $d X x $.
    fillsb.1 $e |- X = U. F $.
    $( A filter is closed under taking supersets.  (Contributed by FL,
       20-Jul-2007.) $)
    fillsb $p |- ( F e. Fil ->
                   ( ( A e. F /\ B C_ X /\ A C_ B ) -> B e. F ) ) $=
      ( vx cfil wcel wss w3a cpw cin c0 wne wa cvv cuni uniexg biimpar wi simp2
      syl5eqel elpw2g syl2an simp1 3adant2 elin sylanbrc ne0i syl adantl jca cv
      elpwg wral cfbas isfil simprbi wceq pweq ineq2d eleq1 imbi12d imp32 sylan
      neeq1d rcla4cv syldan ex ) CGHZACHZBDIZABIZJZBCHZVJVNBDKZHZCBKZLZMNZOZVOV
      JVNOVQVTVJDPHZVLVQVNVJDCQPECGRUBVKVLVMUAWBVQVLBDPUCSUDVNVTVJVNAVSHZVTVNVK
      AVRHZWCVKVLVMUEVKVMWDVLVKWDVMABCUNSUFACVRUGUHVSAUIUJUKULVJCFUMZKZLZMNZWEC
      HZTZFVPUOZWAVOVJCUPHWKFCDEUQURWKVQVTVOWJVTVOTFBVPWEBUSZWHVTWIVOWLWGVSMWLW
      FVRCWEBUTVAVFWEBCVBVCVGVDVEVHVI $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.
    $( A filter is closed under taking intersections.  (Contributed by FL,
       20-Jul-2007.) $)
    filint $p |- ( ( F e. Fil /\ A e. F /\ B e. F ) -> ( A i^i B ) e. F ) $=
      ( vx cfil wcel w3a cv cin wss wrex cfbas filfbas fbasssin syl3an1 wi cuni
      inss1 elssuni syl5ss wa eqid fillsb 3expd imp rexlimdv sylan2 3adant3 mpd
      com23 ) CEFZACFZBCFZGDHZABIZJZDCKZUOCFZUKCLFULUMUQCMDABCNOUKULUQURPZUMULU
      KUOCQZJZUSULUOAUTABRACSTUKVAUAUPURDCUKVAUNCFZUPURPZPUKVBVAVCUKVBVAUPURUNU
      OCUTUTUBUCUDUJUEUFUGUHUI $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d F x $.  $d X x $.
    filusb.1 $e |- X = U. F $.
    $( The underlying set belongs to the filter.  (Contributed by FL,
       20-Jul-2007.) $)
    filusb $p |- ( F e. Fil -> X e. F ) $=
      ( vx cfil wcel c0 wne cfbas filfbas fbasne0 syl cv wex n0 wa wss cuni mpd
      wi elssuni syl6sseqr adantl ssid fillsb 3expd imp mpi ex exlimdv syl5bi )
      AEFZAGHZBAFZULAIFUMAJAKLUMDMZAFZDNULUNDAOULUPUNDULUPUNULUPPZUOBQZUNUPURUL
      UPUOARBUOAUACUBUCUQBBQZURUNTZBUDULUPUSUTTULUPUSURUNUOBABCUEUFUGUHSUIUJUKS
      $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d F x y z $.  $d X x y $.
    isfil2.1 $e |- X = U. F $.
    $( Derive the standard axioms of a filter.  (Contributed by Mario Carneiro,
       27-Nov-2013.) $)
    isfil2 $p |- ( F e. Fil <-> ( ( -. (/) e. F /\ X e. F ) /\
        A. x e. ~P X ( E. y e. F y C_ x -> x e. F ) /\
        A. x e. F A. y e. F ( x i^i y ) e. F ) ) $=
      ( vz wcel c0 wa cv wss wrex wi cpw wral cin w3a elpwi cvv ralimi cfil jca
      filesn filusb fillsb 3expd com23 imp rexlimdv sylan2 ralrimiva ralrimivva
      wn filint 3expb 3jca cfbas wne wnel simp1r pwuni eleq1i pwexg sylbi ssexg
      cuni sylancr syl ne0i simp1l df-nel sylibr sseq1 rcla4ev 3ad2ant3 isfbas2
      ssid mpan2 biimpar syl13anc wex elin anim2i eximi df-rex 3imtr4i 3ad2ant2
      n0 imim1i isfil sylanbrc impbii ) CUAGZHCGUMZDCGZIZBJZAJZKZBCLZWRCGZMZADN
      ZOZWRWQPZCGZBCOZACOZQZWMWPXDXHWMWNWOCUCCDEUDUBWMXBAXCWRXCGWMWRDKZXBWRDRWM
      XJIWSXABCWMXJWQCGZWSXAMZMWMXKXJXLWMXKXJWSXAWQWRCDEUEUFUGUHUIUJUKWMXFABCCW
      MXAXKXFWRWQCUNUOULUPXICUQGZCWRNZPZHURZXAMZAXCOZWMXICSGZCHURZHCUSZFJZXEKZF
      CLZBCOZACOZXMXIWOXSWNWOXDXHUTZWOCCVFZNZKYISGZXSCVAWOYHCGYJDYHCEVBYHCVCVDC
      YISVEVGVHXIWOXTYGCDVIVHXIWNYAWNWOXDXHVJHCVKVLXHWPYFXDXGYEACXFYDBCXFXEXEKZ
      YDXEVQYCYKFXECYBXEXEVMVNVRTTVOXSXMXTYAYFQABFSCVPVSVTXDWPXRXHXBXQAXCXPWTXA
      WQXOGZBWAXKWSIZBWAXPWTYLYMBYLXKWQXNGZIYMWQCXNWBYNWSXKWQWRRWCVDWDBXOWHWSBC
      WEWFWITWGACDEWJWKWL $.
      $( [27-Nov-2013] $)
  $}

  ${
    $d x y A $.  $d z A $.  $d x y F $.  $d y z F $.  $d x y ph $.
    $d y z ph $.  $d y ps $.  $d y B $.
    isfild.1 $e |- ( ph -> ( x e. F <-> ( x C_ A /\ ps ) ) ) $.
    isfild.2 $e |- ( ph -> A e. _V ) $.
    $( Lemma for ~ isfild . $)
    isfildlem $p |- ( ph -> ( B e. F <-> ( B C_ A /\ [ B / x ] ps ) ) ) $=
      ( vy cvv wcel wss wsbc wa wi wb cv wceq eleq1 ax-17 elex a1i ssexg expcom
      syl adantrd sseq1 dfsbcq anbi12d bibi12d imbi2d hbs1 hban hbbi hbim chvar
      sbceq1a vtoclg com12 pm5.21ndd ) AEJKZEFKZEDLZBCEMZNZVBVAOAEFUAUBAVCVAVDA
      DJKZVCVAOHVCVFVAEDJUCUDUEUFVAAVBVEPZAIQZFKZVHDLZBCVHMZNZPZOZAVGOIEJVHERZV
      MVGAVOVIVBVLVEVHEFSVOVJVCVKVDVHEDUGBCVHEUHUIUJUKACQZFKZVPDLZBNZPZOVNCIAVM
      CACTVIVLCVICTVJVKCVJCTBCIULUMUNUOVPVHRZVTVMAWAVQVIVSVLVPVHFSWAVRVJBVKVPVH
      DUGBCVHUQUIUJUKGUPURUSUT $.
      $( [1-Dec-2013] $)

    isfild.3 $e |- ( ph -> [ A / x ] ps ) $.
    $( Base set of a filter of the form ` { x e. ~P A | ph } ` .  (Contributed
       by Mario Carneiro, 14-Dec-2013.) $)
    isfildu $p |- ( ph -> A = U. F ) $=
      ( cuni wcel wss wsbc wa ssid jctil isfildlem mpbird elssuni syl cv syl6bi
      wral simpl ralrimiv unissb sylibr eqssd ) ADEIZADEJZDUHKAUIDDKZBCDLZMAUKU
      JHDNOABCDDEFGPQDERSACTZDKZCEUBUHDKAUMCEAULEJUMBMUMFUMBUCUAUDCEDUEUFUG $.
      $( [14-Dec-2013] $)

    isfild.4 $e |- ( ph -> -. [ (/) / x ] ps ) $.
    isfild.5 $e |- ( ( ph /\ y C_ A /\ z C_ y ) ->
                     ( [ z / x ] ps -> [ y / x ] ps ) ) $.
    isfild.6 $e |- ( ( ph /\ y C_ A /\ z C_ A ) ->
        ( ( [ y / x ] ps /\ [ z / x ] ps ) -> [ ( y i^i z ) / x ] ps ) ) $.
    $( Sufficient condition for a set of the form ` { x e. ~P A | ph } ` to be
       a filter.  (Contributed by Mario Carneiro, 1-Dec-2013.) $)
    isfild $p |- ( ph -> F e. Fil ) $=
      ( c0 wcel wa wss wi wsbc isfildlem wn cuni wrex cpw wral cin simpr syl6bi
      cfil mtod vex elpw biimpri adantr ssrdv sspwuni sylib ssid mpbird elssuni
      cv jctil syl eqssd eqeltrd jca elpwi sseq2d simp2 jctild adantld 3ad2ant1
      w3a wb 3imtr4d 3expa impancom rexlimdva ex syl5 ralrimiv ssinss1 ad2antrr
      sylbid a1i an4 3expb expimpd syl5bi jcad anbi12d ralrimivv eqid syl3anbrc
      isfil2 ) ANGOZUAZGUBZGOZPEVAZDVAZQZEGUCXAGOZRZDWRUDZUEXAWTUFZGOZEGUEDGUEG
      UIOAWQWSAWPBCNSZKAWPNFQZXHPXHABCFNGHITXIXHUGUHUJAWRFGAWRFAGFUDZQWRFQACGXJ
      ACVAZGOXKFQZBPXKXJOZHXLXMBXMXLXKFCUKULUMUNUHUOGFUPUQAFGOZFWRQAXNFFQZBCFSZ
      PAXPXOJFURVBABCFFGHITUSZFGUTVCVDZXQVEVFAXDDXEXAXEOXAWRQZAXDXAWRVGAXSXAFQZ
      XDAWRFXAXRVHAXTXDAXTPZXBXCEGYAXBWTGOZXCAXTXBYBXCRAXTXBVMZWTFQZBCWTSZPZXTB
      CXASZPZYBXCYCYEYHYDYCYEYGXTLAXTXBVIVJVKAXTYBYFVNXBABCFWTGHITZVLAXTXCYHVNX
      BABCFXAGHITZVLVOVPVQVRVSWDVTWAAXGDEGGAYHYFPZXFFQZBCXFSZPXCYBPXGAYKYLYMYKY
      LRAXTYLYGYFXAWTFWBWCWEYKXTYDPZYGYEPZPAYMXTYGYDYEWFAYNYOYMAXTYDYOYMRMWGWHW
      IWJAXCYHYBYFYJYIWKABCFXFGHITVOWLDEGWRWRWMWOWN $.
      $( [1-Dec-2013] $)
  $}

  ${
    $d F x y $.
    $( A filter is closed under taking intersections.  (Contributed by Mario
       Carneiro, 27-Nov-2013.) $)
    filfi $p |- ( F e. Fil -> ( fi ` F ) = F ) $=
      ( vx vy cfil wcel cv cin wral cfi cfv wceq filint 3expib ralrimivv inficl
      mpbid ) ADEZBFZCFZGAEZCAHBAHAIJAKQTBCAAQRAESAETRSALMNBCADOP $.
      $( [27-Nov-2013] $)
  $}

  $( The intersection of two elements of a filter can't be empty.  (Contributed
     by FL, 19-Sep-2007.) $)
  fipfil $p |- ( F e. Fil -> ( ( A e. F /\ B e. F ) ->
    ( A i^i B ) =/= (/) ) ) $=
    ( cfil wcel wa cin c0 wceq wn wne filint 3expb filesn adantr nelneq syl2anc
    ex df-ne syl6ibr ) CDEZACEZBCEZFZABGZHIJZUEHKUAUDUFUAUDFUECEZHCEJZUFUAUBUCU
    GABCLMUAUHUDCNOUEHCPQRUEHST $.
    $( [1-Dec-2010] $) $( [16-Sep-2007] $)

  $( A filter has the finite intersection property.  Bourbaki TG I.36 note of
     def. 1.  (Contributed by FL, 2-Sep-2007.) $)
  fipfil2 $p |- ( F e. Fil -> ( ( A C_ F /\ A =/= (/) /\ A e. Fin ) ->
                  |^| A =/= (/) ) ) $=
    ( cfil wcel wss c0 wne cfn w3a cint cfi cfv elfir ex filfi eleq2d sylibd wn
    wceq filesn eleq1 notbid syl5ibrcom necon2ad syld ) BCDZABEAFGAHDIZAJZBDZUH
    FGUFUGUHBKLZDZUIUFUGUKABCMNUFUJBUHBOPQUFUIUHFUFUIRUHFSZFBDZRBTULUIUMUHFBUAU
    BUCUDUE $.
    $( [27-Nov-2013] $) $( [20-Sep-2007] $)

  $( The empty set is not a filter.  Bourbaki TG I.36 def 1 note.  (Contributed
     by FL, 31-Oct-2007.) $)
  emnfil $p |- ( F e. Fil -> F =/= (/) ) $=
    ( cfil wcel cuni c0 wne eqid filusb ne0i syl ) ABCADZACAEFAKKGHAKIJ $.
    $( [27-Nov-2013] $) $( [30-Oct-2007] $)

  ${
    $d F x y $.  $d G x y $.
    $( The intersection of two filters is a filter.  Use ~ fiint to extend this
       property to the intersection of a finite set of filters.  Bourbaki TG
       I.36 par. 3.  (Contributed by FL, 19-Sep-2007.) $)
    infil $p |- ( ( F e. Fil /\ G e. Fil /\ ( F i^i G ) =/= (/) ) ->
      ( F i^i G ) e. Fil ) $=
      ( vy vx cfil wcel cin c0 w3a wn cuni wa wss wral sseli cvv 3syl eqid imp
      wi wne cv wrex filesn 3ad2ant1 inss1 nsyl inex1g uniexg ssid elpwg mpbiri
      cpw simpl1 simpr2 syl simpr1 elpwi uniss ax-mp sstr mpan2 simpr3 syl13anc
      fillsb simpl2 inss2 sylanbrc 3exp2 rexlimdv ralrimiva elssuni ancli eximi
      elin wex n0 df-rex 3imtr4i 3ad2ant3 wceq sseq2 rexbidv eleq1 rcla4v syl3c
      imbi12d simpl anim12i filint 3expb syl2an simpr ralrimivva 3adant3 isfil2
      jca syl3anbrc ) AEFZBEFZABGZHUAZIZHXAFZJZXAKZXAFZLCUBZDUBZMZCXAUCZXIXAFZT
      ZDXFUMZNZXIXHGZXAFZCXANDXANZXAEFXCXEXGXCHAFZXDWSWTXSJXBAUDUEXAAHABUFZOUGX
      CXFXNFZXOXHXFMZCXAUCZXGWSWTYAXBWSXAPFXFPFZYAABEUHXAPUIYDYAXFXFMXFUJXFXFPU
      KULQUEXCXMDXNXCXIXNFZLXJXLCXAXCYEXHXAFZXJXLTTXCYEYFXJXLXCYEYFXJIZLZXIAFZX
      IBFZXLYHWSXHAFZXIAKZMZXJYIWSWTXBYGUNYHYFYKXCYEYFXJUOZXAAXHXTOZUPYHYEXIXFM
      ZYMXCYEYFXJUQZXIXFURZYPXFYLMZYMXAAMYSXTXAAUSUTXIXFYLVAVBQXCYEYFXJVCZWSYKY
      MXJIYIXHXIAYLYLRVESVDYHWTXHBFZXIBKZMZXJYJWSWTXBYGVFYHYFUUAYNXABXHABVGZOZU
      PYHYEYPUUCYQYRYPXFUUBMZUUCXABMUUFUUDXABUSUTXIXFUUBVAVBQYTWTUUAUUCXJIYJXHX
      IBUUBUUBRVESVDXIABVOVHVISVJVKZXBWSYCWTYFCVPYFYBLZCVPXBYCYFUUHCYFYBXHXAVLV
      MVNCXAVQYBCXAVRVSVTXMYCXGTDXFXNXIXFWAZXKYCXLXGUUIXJYBCXAXIXFXHWBWCXIXFXAW
      DWGWEWFWQUUGWSWTXRXBWSWTLZXQDCXAXAUUJXLYFLZLXPAFZXPBFZXQUUJWSYIYKLUULUUKW
      SWTWHXLYIYFYKXAAXIXTOYOWIWSYIYKUULXIXHAWJWKWLUUJWTYJUUALUUMUUKWSWTWMXLYJY
      FUUAXABXIUUDOUUEWIWTYJUUAUUMXIXHBWJWKWLXPABVOVHWNWODCXAXFXFRWPWR $.
      $( [27-Nov-2013] $) $( [17-Sep-2007] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( A singleton is a filter.  Bourbaki TG I.36, example 1.  (Contributed by
       FL, 19-Sep-2007.) $)
    snfil $p |- ( ( A e. B /\ A =/= (/) ) -> { A } e. Fil ) $=
      ( vx vy wcel c0 wne wa cv wceq wss wb a1i cvv adantr wsbc eqsbc3 ax-mp wi
      w3a csn elsn eqimss pm4.71ri bitri elex eqid mpbiri wn simpr necomd df-ne
      sylib 0ex sylnibr sseq1 anbi2d biimpri syl6bi com12 3adant1 eqsb3 3imtr4g
      eqss cin ineq12 inidm syl6eq anbi12i vex inex1 3imtr4i isfild ) ABEZAFGZH
      ZCIZAJZCDCAAUAZVQVSEZVQAKZVRHZLVPVTVRWBCAUBVRWAVQAUCUDUEMVNANEVOABUFOVNVR
      CAPZVOVNWCAAJAUGCAABQUHOVPFAJZVRCFPZVPFAGWDUIVPAFVNVOUJUKFAULUMFNEWEWDLUN
      CFANQRUOVPDIZAKZVQWFKZTVRWFAJZVRCVQPZVRCWFPZWGWHVRWISVPVRWGWHHZWIVRWLWGAW
      FKZHZWIVRWHWMWGVQAWFUPUQWIWNWFAVDURUSUTVACCAVBZDCAVBZVCWKWJHZVRCWFVQVEZPZ
      SVPWGWATWIVRHZWRAJZWQWSWTWRAAVEAWFAVQAVFAVGVHWKWIWJVRWPWOVIWRNEWSXALWFVQD
      VJVKCWRANQRVLMVM $.
      $( [1-Dec-2013] $) $( [16-Sep-2007] $)
  $}

  ${
    $d x y z A $.  $d x y V $.
    $( A condition for a set to generate a filter base.  (Contributed by Jeff
       Hankins, 2-Sep-2009.) $)
    fsubbas $p |- ( A e. V -> ( ( fi ` A ) e. fBas <->
                    ( A =/= (/) /\ -. (/) e. ( fi ` A ) ) ) ) $=
      ( vz vx vy wcel cfi cfv cfbas c0 wne wn wa fbasne0 fi0 w3a wss wral cvv
      cv necon3bid biimpar sylan2 0nelfb adantl jca ex wnel wrex biimpa 3adant3
      cin simp3 df-nel sylibr fiin ssid sseq1 rcla4ev sylancl rgen2a wb isfbas2
      a1i fvex ax-mp syl3anbrc 3expib impbid ) ABFZAGHZIFZAJKZJVKFLZMZVJVLVOVJV
      LMVMVNVLVJVKJKZVMVKNVJVMVPVJAJVKJABOUAZUBUCVLVNVJVKUDUEUFUGVJVMVNVLVJVMVN
      PZVPJVKUHZCTZDTZETZULZQZCVKUIZEVKRDVKRZVLVJVMVPVNVJVMVPVQUJUKVRVNVSVJVMVN
      UMJVKUNUOWFVRWEDEVKWAVKFWBVKFMWCVKFWCWCQZWEWAWBAUPWCUQWDWGCWCVKVTWCWCURUS
      UTVAVDVKSFVLVPVSWFPVBAGVEDECSVKVCVFVGVHVI $.
      $( [25-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x y z F $.
    $( A filter base has the finite intersection property.  (Contributed by
       Jeff Hankins, 2-Sep-2009.) $)
    fbasfip $p |- ( F e. fBas -> -. (/) e. ( fi ` F ) ) $=
      ( vy vz cfbas wcel c0 cfi cfv cv cint wceq cpw cfn wrex wa wss wn wi mtod
      cvv cin elin elpwi anim1i sylbi fbssint 3expb sylan2 0nelfb eleq1 biimpcd
      ad2antrr adantl ss0 adantrr sseq2 biimprcd ad2antll exp32 rexlimdv nrexdv
      nsyl mpd wb 0ex elfi mpan mtbird ) ADEZFAGHEZFBIZJZKZBALZMUAZNZVIVMBVOVIV
      KVOEZOZCIZVLPZCANZVMQZVQVIVKAPZVKMEZOZWAVQVKVNEZWDOWEVKVNMUBWFWCWDVKAUCUD
      UEVIWCWDWACVKAUFUGUHVRVTWBCAVRVSAEZVTWBVRWGVTOOVMVSFPZVRWGWHQVTVRWGOZVSFK
      ZWHWIWJFAEZVIWKQVQWGAUIULWGWJWKRVRWJWGWKVSFAUJUKUMSVSUNVBUOVTVMWHRVRWGVMW
      HVTFVLVSUPUQURSUSUTVCVAFTEVIVJVPVDVEBFATDVFVGVH $.
      $( [25-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d w x y z G $.  $d x y z F $.
    $( A helpful lemma for showing that certain sets generate filters.
       (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fbunfip $p |- ( ( F e. fBas /\ G e. fBas ) ->
                    ( -. (/) e. ( fi ` ( F u. G ) ) <->
                      A. x e. F A. y e. G ( x i^i y ) =/= (/) ) ) $=
      ( vz vw cfbas wcel wa c0 cfi wn cv wceq wrex wne wral wss wi syl cbvral2v
      cun cfv cin w3o elfiun notbid w3a 3ioran df-3an syl6bbr necom df-ne bitri
      bitr2i ralbii ralnex fbasfip anim12i biantrurd syl5rbb ssfii ssralv ineq1
      ralimdv sylan9 neeq1d ineq2 fbssfi r19.29 ss2in sseq2 ss0 syl5com necon3d
      syl6bi ex com13 rexlimivw impancom expimpd com12 syl2an ralrimdvva syl5bi
      imp an4s impbid 3bitrd ) CGHZDGHZIZJCDUBKUCHZLZJCKUCZHZLZJDKUCZHZLZIZJAMZ
      BMZUDZNZBWROZAWOOZLZIZXDJPZBWRQZAWOQZXJBDQZACQZWLWNWPWSXGUEZLZXIWLWMXOABJ
      CDGGUFUGXPWQWTXHUHXIWPWSXGUIWQWTXHUJUOUKXLXHWLXIXLXFLZAWOQXHXKXQAWOXKXELZ
      BWRQXQXJXRBWRXJJXDPXRXDJULJXDUMUNUPXEBWRUQUNUPXFAWOUQUNWLXAXHWJWQWKWTCURD
      URUSUTVAWLXLXNWJXLXKACQZWKXNWJCWORXLXSSCGVBXKACWOVCTWKXKXMACWKDWRRXKXMSDG
      VBXJBDWRVCTVEVFXNEMZFMZUDZJPZFDQZECQZWLXLXJYCXTXCUDZJPABEFCDXBXTNXDYFJXBX
      TXCVDVGXCYANYFYBJXCYAXTVHVGUAWLYEXJABWOWRWJXBWOHZWKXCWRHZYEXJSZWJYGIXTXBR
      ZECOZYAXCRZFDOZYIWKYHIEXBCVIFXCDVIYEYKYMIXJYEYKYMXJYEYKIYDYJIZECOYMXJSZYD
      YJECVJYNYOECYDYMYJXJYDYMIYCYLIZFDOYJXJSZYCYLFDVJYPYQFDYCYLYQYJYLYCXJYJYLY
      CXJSYJYLIZXDJYBJYRYBXDRZXDJNZYBJNZXTXBYAXCVKYTYSYBJRUUAXDJYBVLYBVMVPVNVOV
      QVRWFVSTVTVSTWAWBWCWGWDWEWHWI $.
      $( [26-Nov-2013] $) $( [3-Sep-2009] $)
  $}

  ${
    $d f x F $.  $d f x X $.
    fgf.1 $e |- X = U. F $.
    $( The filter generating class gives a filter for every filter base.
       (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fgf $p |- ( F e. fBas -> ( filGen ` F ) = { x e. ~P X | ( F i^i ~P x ) =/=
    (/) } ) $=
      ( vf cfbas wcel cvv cv cpw cin c0 wne crab cfg cfv wceq elex cuni uniexg
      syl5eqel pwexg rabexg 3syl unieq syl6eqr syl ineq1 neeq1d rabeqbidv df-fg
      pweq fvmptg syl2anc ) BFGZBHGBAIJZKZLMZACJZNZHGZBOPUTQBFRUOCHGUSHGVAUOCBS
      ZHDBFTUACHUBURAUSHUCUDEBEIZUPKZLMZAVCSZJZNUTHHOVCBQZVEURAVGUSVHVFCQVGUSQV
      HVFVBCVCBUEDUFVFCULUGVHVDUQLVCBUPUHUIUJEAUKUMUN $.
      $( [26-Nov-2013] $) $( [3-Sep-2009] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y F $.  $d y X $.  $d x y $.
    elfg.1 $e |- X = U. F $.
    $( A condition for elements of a generated filter.  (Contributed by Jeff
       Hankins, 3-Sep-2009.) $)
    elfg $p |- ( F e. fBas -> ( A e. ( filGen ` F ) <-> ( A C_ X
    /\ E. x e. F x C_ A ) ) ) $=
      ( vy cfbas wcel cfg cfv cv cpw cin c0 wne wss wa cvv wb wex crab wrex fgf
      eleq2d wceq pweq ineq2d neeq1d elrab cuni uniexg syl5eqel elpw2g syl elin
      vex elpw anbi2i bitri exbii n0 df-rex 3bitr4i a1i anbi12d syl5bb bitrd )
      CGHZBCIJZHBCFKZLZMZNOZFDLZUAZHZBDPZAKZBPZACUBZQZVHVIVOBFCDEUCUDVPBVNHZCBL
      ZMZNOZQVHWAVMWEFBVNVJBUEZVLWDNWFVKWCCVJBUFUGUHUIVHWBVQWEVTVHDRHWBVQSVHDCU
      JRECGUKULBDRUMUNWEVTSVHVRWDHZATVRCHZVSQZATWEVTWGWIAWGWHVRWCHZQWIVRCWCUOWJ
      VSWHVRBAUPUQURUSUTAWDVAVSACVBVCVDVEVFVG $.
      $( [1-Dec-2010] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.
    $( A filter base is a subset of its generated filter.  (Contributed by Jeff
       Hankins, 3-Sep-2009.) $)
    fbssfg $p |- ( F e. fBas -> F C_ ( filGen ` F ) ) $=
      ( vt vx cfbas wcel cfg cfv cv cuni wss wrex wa elssuni ssid sseq1 rcla4ev
      mpan2 jca eqid elfg syl5ibr ssrdv ) ADEZBAAFGZBHZAEZUEUDEUCUEAIZJZCHZUEJZ
      CAKZLUFUHUKUEAMUFUEUEJZUKUENUJULCUEAUIUEUEOPQRCUEAUGUGSTUAUB $.
      $( [1-Dec-2010] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x y F $.  $d t x y X $.
    fgbas.1 $e |- X = U. F $.
    $( The base set of a generated filter is the base set of the parent base.
       (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fgbas $p |- ( F e. fBas -> X = U. ( filGen ` F ) ) $=
      ( vt vx vy cfbas wcel cfg cfv cuni wss fbssfg uniss syl syl5eqss cv eluni
      wa wex wrex anbi2d ssel2 ancoms adantrr syl6bi exlimdv syl5bi ssrdv eqssd
      elfg ) AGHZBAIJZKZULBAKZUNCULAUMLUOUNLAMAUMNOPULDUNBDQZUNHUPEQZHZUQUMHZSZ
      ETULUPBHZEUPUMRULUTVAEULUTURUQBLZFQUQLFAUAZSZSVAULUSVDURFUQABCUKUBURVBVAV
      CVBURVAUQBUPUCUDUEUFUGUHUIUJ $.
      $( [1-Dec-2010] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.  $d t x G $.
    $( A bigger base generates a bigger filter. $)
    fgss $p |- ( ( F e. fBas /\ G e. fBas /\ F C_ G ) -> ( filGen ` F ) C_ (
    filGen ` G ) ) $=
      ( vt vx cfbas wcel wss w3a cfg cfv cv cuni wrex wa wi uniss sstr2 wb eqid
      elfg syl5com ssrexv anim12d 3ad2ant3 3ad2ant1 3ad2ant2 3imtr4d ssrdv ) AE
      FZBEFZABGZHZCAIJZBIJZULCKZALZGZDKUOGZDAMZNZUOBLZGZURDBMZNZUOUMFZUOUNFZUKU
      IUTVDOUJUKUQVBUSVCUKUPVAGUQVBABPUOUPVAQUAURDABUBUCUDUIUJVEUTRUKDUOAUPUPST
      UEUJUIVFVDRUKDUOBVAVASTUFUGUH $.
      $( [5-Sep-2009] $)
  $}

  ${
    $d t u v x y F $.  $d t u v x y G $.  $d t u v x y X $.  $d t u v x y Y $.
    $d t u v x y $.
    fbfgss.1 $e |- X = U. F $.
    fbfgss.2 $e |- Y = U. G $.
    $( A condition for a filter to be finer than another involving their filter
       bases.  (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fbfgss $p |- ( ( F e. fBas /\ G e. fBas /\ X = Y ) -> ( ( filGen ` F ) C_ (
    filGen ` G ) <-> A. x e. F E. y e. G y C_ x ) ) $=
      ( vt vu vv cfbas wcel wceq wss cv wrex wa wi elfg w3a cfg cfv wral fbssfg
      3ad2ant1 sseld ssel2 simpr syl6bi 3ad2ant2 syl5 exp3a syl5d sseq2 rexbidv
      ralrimdv rcla4v adantl simpll3 sseq2d sstr sseq1 rcla4ev a1d sylanr2 jcad
      biimpd exp45 rexlimdv impancom com23 imp3a wb adantr 3imtr4d ssrdv impbid
      syld ex ) CLMZDLMZEFNZUAZCUBUCZDUBUCZOZBPZAPZOZBDQZACUDZWDWGWKACWDWICMWIW
      EMZWGWKWDCWEWIWAWBCWEOWCCUEUFUGWDWGWMWKWGWMRWIWFMZWDWKWEWFWIUHWBWAWNWKSWC
      WBWNWIFOZWKRWKBWIDFHTWOWKUIUJUKULUMUNUQWDWLWGWDWLRZIWEWFWPIPZEOZJPZWQOZJC
      QZRZWQFOZKPZWQOZKDQZRZWQWEMZWQWFMZWPWRXAXGWPXAWRXGWPWTWRXGSZJCWDWSCMZWLWT
      XJSZWDXKRZWLWHWSOZBDQZXLXKWLXOSWDWKXOAWSCWIWSNWJXNBDWIWSWHUOUPURUSXMXNXLB
      DXMWHDMZXNWTXJXMXPXNWTRZRZRZWRXCXFXSWRXCXSEFWQWAWBWCXKXRUTVAVHXQXMXPWHWQO
      ZWRXFSWHWSWQVBXMXPXTRZRXFWRYAXFXMXEXTKWHDXDWHWQVCVDUSVEVFVGVIVJVSVKVJVLVM
      WDXHXBVNZWLWAWBYBWCJWQCEGTUFVOWDXIXGVNZWLWBWAYCWCKWQDFHTUKVOVPVQVTVR $.
      $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.
    $( A filter generates itself.  (Contributed by Jeff Hankins,
       5-Sep-2009.) $)
    fgid $p |- ( F e. Fil -> ( filGen ` F ) = F ) $=
      ( vt vx cfil wcel cfg cfv cv cuni wss wrex wa cfbas filfbas eqid elfg syl
      wb wi fillsb 3expd com34 rexlimdv com23 imp3a sylbid ssrdv fbssfg eqssd )
      ADEZAFGZAUJBUKAUJBHZUKEZULAIZJZCHZULJZCAKZLZULAEZUJAMEZUMUSRANZCULAUNUNOZ
      PQUJUOURUTUJURUOUTUJUQUOUTSCAUJUPAEZUOUQUTUJVDUOUQUTUPULAUNVCTUAUBUCUDUEU
      FUGUJVAAUKJVBAUHQUI $.
      $( [5-Sep-2009] $)
  $}

  ${
    $d u v w y z ph $.  $d u v z F $.  $d u v x y z X $.  $d u w x y z B $.
    fgfild.1 $e |- ( ph -> ( ( B e. fBas /\ U. B C_ X /\ X e. _V ) /\
                       F = { x e. ~P X | E. y e. B y C_ x } ) ) $.
    $( Lemma for ~ fgfil . $)
    fgfild $p |- ( ph -> ( F e. Fil /\ X = U. F ) ) $=
      ( vz wcel wceq cv wss wrex wa sseq2 rexbidv wsbc c0 syl wi vu vv cfil cpw
      cuni crab cfbas cvv w3a simprd eleq2d elrab vex elpw anbi1i syl6bb simpld
      vw bitri simp3d wex wne simp1d fbasne0 sylib elssuni simp2d sstr2 syl5com
      n0 syl5 ancld eximdv mpd df-rex sylibr wb sbcieg mpbird wn 0nelfb 0ex ss0
      sbcie eleq1d biimpac rexlimiva sylbi nsyl expcom reximdv 3ad2ant3 3imtr4g
      sstr cin fbasssin 3expib ss2in com12 exp5c impancom rexlimdv ex rexlimdva
      syl6 imp31 imp3a 3ad2ant1 sseq1 cbvrexv anbi12i inex1 isfild isfildu jca
      ) AEUCIFEUEJACKZHKZLZCDMZHUAUBFEAXQEIXQXPBKZLZCDMZBFUDZUFZIZXQFLZXSNZAEYD
      XQADUGIZDUEZFLZFUHIZUIZEYDJZGUJUKYEXQYCIZXSNYGYBXSBXQYCXTXQJYAXRCDXTXQXPO
      PULYNYFXSXQFHUMUNUOUSUPZAYHYJYKAYLYMGUQZUTZAXSHFQZXPFLZCDMZAXPDIZYSNZCVAZ
      YTAUUACVAZUUCADRVBZUUDAYHUUEAYHYJYKYPVCZDVDSCDVJVEAUUAUUBCAUUAYSUUAXPYILZ
      AYSXPDVFAYJUUGYSAYHYJYKYPVGXPYIFVHVIVKVLVMVNYSCDVOVPAYKYRYTVQYQXSYTHFUHXQ
      FJXRYSCDXQFXPOPVRSVSZARDIZXSHRQZAYHUUIVTUUFDWASUUJXPRLZCDMZUUIXSUULHRWBXQ
      RJXRUUKCDXQRXPOPWDUUKUUICDUUKUUAUUIUUKXPRDXPWCWEWFWGWHWIAUAKZFLZUBKZUUMLZ
      UIXPUUOLZCDMZXPUUMLZCDMZXSHUUOQZXSHUUMQZUUPAUURUUTTUUNUUPUUQUUSCDUUQUUPUU
      SXPUUOUUMWNWJWKWLXSUURHUUOUBUMXQUUOJXRUUQCDXQUUOXPOPWDZXSUUTHUUMUAUMZXQUU
      MJXRUUSCDXQUUMXPOPWDZWMAUUNUUOFLZUIXQUUMLZHDMZURKZUUOLZURDMZNZXPUUMUUOWOZ
      LZCDMZUVBUVANXSHUVMQAUUNUVLUVOTUVFAUVHUVKUVOAUVGUVKUVOTZHDAXQDIZNZUVGUVPU
      VRUVGNUVJUVOURDUVRUVIDIZUVGUVJUVOTZAUVQUVSUVGUVTTAUVQUVSUVGUVJUVOAUVQUVSN
      ZXPXQUVIWOZLZCDMZUVGUVJNZUVOTAYHUWAUWDTUUFYHUVQUVSUWDCXQUVIDWPWQSUWEUWDUV
      OUWEUWBUVMLZUWDUVOTXQUUMUVIUUOWRUWFUWCUVNCDUWCUWFUVNXPUWBUVMVHWSWKSWSXEWT
      XFXAXBXCXDXGXHUVBUVHUVAUVKUVBUUTUVHUVEUUSUVGCHDXPXQUUMXIXJUSUVAUURUVKUVCU
      UQUVJCURDXPUVIUUOXIXJUSXKXSUVOHUVMUUMUUOUVDXLXQUVMJXRUVNCDXQUVMXPOPWDWMXM
      AXSHFEYOYQUUHXNXO $.
      $( [30-Nov-2013] $)
  $}

  ${
    $d x y z F $.
    $( A generated filter is a filter.  (Contributed by Jeff Hankins,
       3-Sep-2009.)  (Revised by Mario Carneiro, 28-Nov-2013.) $)
    fgfil $p |- ( F e. fBas -> ( filGen ` F ) e. Fil ) $=
      ( vx vy vz cfbas wcel cfg cfv cfil cuni wceq wss cvv w3a cv wrex cpw crab
      id wa ssid a1i uniexg 3jca eqid elfg sseq2 rexbidv elrab vex anbi1i bitri
      elpw syl6bbr eqrdv jca fgfild simpld ) AEFZAGHZIFAJZUTJKUSBCAUTVAUSUSVAVA
      LZVAMFZNUTCOZBOZLZCAPZBVAQZRZKUSUSVBVCUSSVBUSVAUAUBAEUCUDUSDUTVIUSDOZUTFV
      JVALZVDVJLZCAPZTZVJVIFZCVJAVAVAUEUFVOVJVHFZVMTVNVGVMBVJVHVEVJKVFVLCAVEVJV
      DUGUHUIVPVKVMVJVADUJUMUKULUNUOUPUQUR $.
      $( [28-Nov-2013] $) $( [3-Sep-2009] $)
  $}

  ${
    $d x y F $.
    $( A filter gives rise to a connected topology.  (Contributed by Jeff
       Hankins, 6-Dec-2009.)  (Revised by Mario Carneiro, 14-Dec-2013.) $)
    filcon $p |- ( F e. Fil -> ( F u. { (/) } ) e. Con ) $=
      ( vx vy wcel c0 cun cin cuni wceq wa wo wi wss wral cvv syl wn elun eleq1
      ex cfil csn ccon ccld cfv cpr cv cdif ctop wal wb p0ex unexg mpan2 istopg
      wex nss simpll ssel2 adantll sylib orcomd impr uniss ad2antlr uniun unisn
      ord 0ex uneq2i un0 3eqtrri syl6sseqr elssuni ad2antrl w3a eqid fillsb imp
      syl13anc elun1 exlimdv syl5bi uni0b sselii mpbiri sylbir pm2.61d2 alrimiv
      ssun2 snid filint 3expa ralrimiva elsni ineq2 in0 syl6eq syl6eqel sylancl
      rgen ralun ineq1 0ss df-ss ralrimivw mpbir2and cldopn adantr orim2i sylbi
      mpbi cfbas filfbas fbncp sylan pm2.21d a1d a1i jaod adantl ssdif0 biimpri
      eqss simplbi2 syl2im orim12d syl5 expimpd jctil anbi12d syl5ibrcom filusb
      syld 0cld topcld jca impbid elin vex eqeq2i orbi2i bitr4i 3bitr4g mpbird
      elpr eqrdv iscon ) AUADZAEUBZFZUCDZUUKUUKUDUEZGZEUUKHZUFZIZUUIBUUNUUPUUIB
      UGZUUKDZUURUUMDZJZUUREIZUURAHZIZKZUURUUNDUURUUPDZUUIUVAUVEUUIUUSUUTUVEUUI
      UUSJZUUTUVCUURUHZUUKDZUVEUUIUUTUVILZUUSUUIUUKUIDZUVJUUIUVKUURUUKMZUURHZUU
      KDZLZBUJZUURCUGZGZUUKDZCUUKNZBUUKNZUUIUUKODZUVKUVPUWAJUKUUIUUJODUWBULAUUJ
      UAOUMUNBCOUUKUOPUUIUVOBUUIUVLUVNUUIUVLJZUURUUJMZUVNUWDQUVQUURDZUVQUUJDZQZ
      JZCUPUWCUVNCUURUUJUQUWCUWHUVNCUWCUWHUVNUWCUWHJZUVMADZUVNUWIUUIUVQADZUVMUV
      CMZUVQUVMMZUWJUUIUVLUWHURUWCUWEUWGUWKUWCUWEJZUWFUWKUWNUWKUWFUWNUVQUUKDZUW
      KUWFKUVLUWEUWOUUIUURUUKUVQUSUTUVQAUUJRVAVBVHVCUWIUVMUUOUVCUVLUVMUUOMUUIUW
      HUURUUKVDVEUUOUVCUUJHZFUVCEFUVCAUUJVFUWPEUVCEVIVGVJUVCVKVLZVMUWEUWMUWCUWG
      UVQUURVNVOUUIUWKUWLUWMVPUWJUVQUVMAUVCUVCVQZVRVSVTUVMAUUJWAPTWBWCUWDUVMEIZ
      UVNUURWDUWSUVNEUUKDZUUJUUKEUUJAWJEVIWKWEZUVMEUUKSWFWGWHTWIUUIUVTBANUVTBUU
      JNUWAUUIUVTBAUUIUURADZJZUVSCANUVSCUUJNUVTUXCUVSCAUUIUXBUWKUVSUUIUXBUWKVPU
      VRADUVSUURUVQAWLUVRAUUJWAPWMWNUVSCUUJUWFUVQEIZUVSUVQEWOUXDUVREUUKUXDUVRUU
      REGEUVQEUURWPUURWQWRUXAWSPXAUVSCAUUJXBWTWNUVTBUUJUURUUJDZUVBUVTUUREWOZUVB
      UVSCUUKUVBUVREUUKUVBUVREUVQGZEUUREUVQXCEUVQMUXGEIUVQXDEUVQXEXLWRUXAWSXFPX
      AUVTBAUUJXBWTXGZUVKUUTUVIUURUUKUVCUWQXHTPXIUVIUVHADZUVHEIZKZUVGUVEUVIUXIU
      VHUUJDZKUXKUVHAUUJRUXLUXJUXIUVHEWOXJXKUVGUXIUVBUXJUVDUUIUUSUXIUVBLZUUSUXB
      UXEKUUIUXMUURAUUJRUUIUXBUXMUXEUUIUXBUXMUXCUXIUVBUUIAXMDUXBUXIQAXNUURUVCAX
      OXPXQTUXEUXMLUUIUXEUVBUXIUXFXRXSXTWCVSUVGUURUVCMZUXJUVCUURMZUVDUUSUXNUUIU
      USUURUUOUVCUURUUKVNUWQVMYAUXOUXJUVCUURYBYCUVDUXNUXOUURUVCYDYEYFYGYHYNYIUU
      IUVBUVAUVDUUIUVAUVBUWTEUUMDZJUUIUXPUWTUUIUVKUXPUXHUUKYOPUXAYJUVBUUSUWTUUT
      UXPUUREUUKSUUREUUMSYKYLUUIUVAUVDUVCUUKDZUVCUUMDZJUUIUXQUXRUUIUVCADUXQAUVC
      UWRYMUVCAUUJWAPUUIUVKUXRUXHUUKUVCUWQYPPYQUVDUUSUXQUUTUXRUURUVCUUKSUURUVCU
      UMSYKYLXTYRUURUUKUUMYSUVFUVBUURUUOIZKUVEUUREUUOBYTUUFUVDUXSUVBUVCUUOUURUW
      QUUAUUBUUCUUDUUGUUIUVKUULUUQUKUXHUUKUUHPUUE $.
      $( [14-Dec-2013] $) $( [6-Dec-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z F $.  $d x y X $.
    extbas.1 $e |- X = U. F $.
    $( A way to extend the base set of a filter.  (Contributed by Jeff Hankins,
       6-Sep-2009.) $)
    extbas1 $p |- ( ( F e. fBas /\ X C_ A ) -> ( F u. { A } ) e. fBas ) $=
      ( vz vx vy cfbas wcel wss wa cvv cun cv wrex wral simprl wceq adantr c0
      csn cin ssun1 cuni elssuni syl6sseqr sylan9ssr elsni ad2antll df-ss sylib
      sseqtr4d eqimss2 syl sseq1 rcla4ev syl2anc ssrexv mpsyl ralrimivva wb wne
      id fbdmn0 ssn0 syl2anr cfil snfil filfbas anasss fbun syldan mpbird wn wi
      expr snprc uneq2 sylbi un0 syl6req eleq1d biimpcd pm2.61d ) BHIZCAJZKZALI
      ZBAUAZMZHIZWEWFWHWKWEWFWHKZKZWKENZFNZGNZUBZJZEWJOZGWIPFBPZWMWSFGBWIBWJJWM
      WOBIZWPWIIZKZKZWREBOZWSBWIUCXDXAWOWQJZXEWMXAXBQXDWQWORZXFXDWOWPJXGXDWOAWP
      XCWMWOCAXAWOCJXBXAWOBUDCWOBUEDUFSWEWFWHQUGXBWPARWMXAWPAUHUIULWOWPUJUKWOWQ
      UMUNWRXFEWOBWNWOWQUOUPUQWREBWJURUSUTWEWLWIHIZWKWTVAWEWFWHXHWHWHATVBZXHWGW
      HVCWFWFCTVBXIWEWFVCBCDVDCAVEVFWHXIKWIVGIXHALVHWIVIUNVFVJFGEBWIVKVLVMVPWEW
      HVNZWKVOWFXJWEWKXJBWJHXJWJBTMZBXJWITRWJXKRAVQWITBVRVSBVTWAWBWCSWD $.
      $( [28-Nov-2013] $) $( [6-Sep-2009] $)

    $( The base set of an extended filter.  (Contributed by Jeff Hankins,
       7-Sep-2009.) $)
    extbas2 $p |- ( ( X C_ A /\ A e. B ) -> U. ( F u. { A } ) =
      A ) $=
      ( wss wcel csn cun cuni uniun uneq1i eqtr4i unisng uneq2d ssid jctr sylib
      wa unss ssun2 a1i eqssd sylan9eqr syl5eq ) DAFZABGZSCAHZIJZDUHJZIZAUICJZU
      JIUKCUHKDULUJELMUGUFUKDAIZAUGUJADABNOUFUMAUFUFAAFZSUMAFUFUNAPQDAATRAUMFUF
      ADUAUBUCUDUE $.
      $( [1-Dec-2010] $) $( [7-Sep-2009] $)
  $}

  ${
    $d r s u v w x y z B $.  $d r s u v w z C $.  $d r s u v w x y z F $.
    $d r s u v w x X $.
    filrn.1 $e |- X = U. B $.
    filrn.2 $e |- C = { y | E. x e. B y = ( F " x ) } $.
    $( Given a filter on a domain, produce a filter on the range.  (Contributed
       by Jeff Hankins, 7-Sep-2009.) $)
    filrn $p |- ( ( B e. fBas /\ F Fn X ) -> C e. fBas ) $=
      ( vr vu vv wcel wa c0 cv wss wrex cvv wceq wn vz vs vw cfbas wfn wne wnel
      cin wral w3a cima cab abrexexg syl5eqel isfbas2 syl adantr cmpt crn rnmpt
      wb eqid eqtr4i a1i cdm wfun fnfun adantl funimaexg ralrimiva 3syl fbasne0
      dmmptg eqnetrd dm0rn0 necon3bii sylib cuni wi elssuni 0nelfb eleq1 notbid
      syl5ibrcom con2d jcad syl6eq sseq2d biimpar sseqin2 eqeq1d biimpd expimpd
      fndm con3d eqcom imadisj bitri notbii syl6ibr ralrimiv eleq2i 0ex rexbidv
      syld eqeq1 elab bitr2i ralnex df-nel 3bitr4i imaeq2 eqeq2d cbvrexv 3bitri
      vex anbi12i reeanv bitr4i fbasssin 3expb ad2ant2r mpan2 ad2antrl funimaex
      rcla4ev elabg syl5bb ad2antrr mpbird imass2 ad2antll inss1 ax-mp ad2antlr
      inss2 ssini ineq12 syl5sseqr exp32 sstrd sseq1 syl2anc adantlrl rexlimdvv
      rexlimdv mpd syl5bi ralrimivv mpbir3and ) CUDLZEFUEZMZDUDLZDNUFZNDUGZUAOZ
      IOZUBOZUHZPZUADQZUBDUIIDUIZUUKUUNUUOUUPUVCUJVAZUULUUKDRLUVDUUKDBOZEAOZUKZ
      SZACQZBULZRHABCUVGUDUMUNIUBUARDUOUPUQUUMDACUVGURZUSZNDUVLSUUMDUVJUVLHABCU
      VGUVKUVKVBUTVCVDUUMUVKVEZNUFUVLNUFUUMUVMCNUUMEVFZUVGRLZACUIUVMCSUULUVNUUK
      FEVGZVHUVNUVOACEUVFCVIVJACUVGRVMVKUUKCNUFUULCVLUQVNUVMNUVLNUVKVOVPVQVNUUM
      NUVGSZTZACUIZUUPUUMUVRACUUMUVFCLZUVFCVRZPZUVFNSZTZMZUVRUUMUVTUWBUWDUVTUWB
      VSUUMUVFCVTVDUUKUVTUWDVSUULUUKUWCUVTUUKUVTTUWCNCLZTCWAUWCUVTUWFUVFNCWBWCW
      DWEUQWFUUMUWEEVEZUVFUHZNSZTZUVRUUMUWBUWDUWJUUMUWBMZUWIUWCUWKUWIUWCUWKUWHU
      VFNUWKUVFUWGPZUWHUVFSUUMUWLUWBUUMUWGUWAUVFUUMUWGFUWAUULUWGFSUUKFEWNVHGWGW
      HWIUVFUWGWJVQWKWLWOWMUVQUWIUVQUVGNSUWINUVGWPEUVFWQWRWSWTXEXAUVQACQZTNDLZT
      UVSUUPUWMUWNUWNNUVJLUWMDUVJNHXBUVIUWMBNXCUVENSUVHUVQACUVENUVGXFXDXGXHWSUV
      QACXINDXJXKVQUUMUVBIUBDDUURDLZUUSDLZMZUUREJOZUKZSZUUSEKOZUKZSZMZKCQJCQZUU
      MUVBUWQUWTJCQZUXCKCQZMUXEUWOUXFUWPUXGUWOUURUVJLUURUVGSZACQZUXFDUVJUURHXBU
      VIUXIBUURIXPUVEUURSUVHUXHACUVEUURUVGXFXDXGUXHUWTAJCUVFUWRSUVGUWSUURUVFUWR
      EXLXMXNXOUWPUUSUVJLUUSUVGSZACQZUXGDUVJUUSHXBUVIUXKBUUSUBXPUVEUUSSUVHUXJAC
      UVEUUSUVGXFXDXGUXJUXCAKCUVFUXASUVGUXBUUSUVFUXAEXLXMXNXOXQUWTUXCJKCCXRXSUU
      MUXDUVBJKCCUUMUWRCLZUXACLZMZUXDUVBUUMUXNUXDMMZUCOZUWRUXAUHZPZUCCQZUVBUUKU
      XNUXSUULUXDUUKUXLUXMUXSUCUWRUXACXTYAYBUXOUXRUVBUCCUXOUXPCLZUXRUVBUUMUXDUX
      TUXRMZUVBUXNUUMUXDMZUYAMZEUXPUKZDLZUYDUUTPZUVBUYCUYEUYDUVGSZACQZUXTUYHUYB
      UXRUXTUYDUYDSZUYHUYDVBUYGUYIAUXPCUVFUXPSUVGUYDUYDUVFUXPEXLXMYFYCYDUUMUYEU
      YHVAZUXDUYAUULUYJUUKUYEUYDUVJLZUULUYHDUVJUYDHXBUULUVNUYDRLUYKUYHVAUVPEUXP
      UCXPYEUVIUYHBUYDRUVEUYDSUVHUYGACUVEUYDUVGXFXDYGVKYHVHYIYJUYCUYDEUXQUKZUUT
      UXRUYDUYLPUYBUXTUXPUXQEYKYLUYCUWSUXBUHZUYLUUTUYLUWSUXBUXQUWRPUYLUWSPUWRUX
      AYMUXQUWREYKYNUXQUXAPUYLUXBPUWRUXAYPUXQUXAEYKYNYQUXDUUTUYMSUUMUYAUURUWSUU
      SUXBYRYOYSUUAUVAUYFUAUYDDUUQUYDUUTUUBYFUUCUUDYTUUFUUGYTUUEUUHUUIUUJ $.
      $( [28-Nov-2013] $) $( [7-Sep-2009] $)
  $}

  ${
    $d f g h x y F $.  $d f g x y X $.
    $( The union of a nonempty set of filters with a common base and closed
       under pairwise union is a filter.  (Contributed by Mario Carneiro,
       28-Nov-2013.) $)
    filuni $p |- ( ( F C_ Fil /\ F =/= (/) /\ A. f e. F ( U. f = X /\
           A. g e. F ( f u. g ) e. F ) ) -> ( U. F e. Fil /\ X = U. U. F ) ) $=
      ( vx vh cfil wss c0 cv wcel wa w3a wrex eluni2 wi syl ex cvv wsbc vy cuni
      wne wceq wral r19.29 simpl elssuni sseq2 biimpa syl2an rexlimivw 3ad2ant3
      cun syl5bi pm4.71rd r19.2z vex uniex eleq1 mpbii adantr ssel2 eqid filusb
      3adant1 wb syl5ibcom ralimdva sylan9 3impia sylibr sbcel1gv mpbird filesn
      wn ralrimiva 3ad2ant1 0ex ax-mp bitri notbii ralnex bitr4i simp13 simprrr
      simp1 simpl2 simprll adantl sseqtr4d simpl3 fillsb syl13anc expr reximdva
      imp syl3an1 syld clelsb3 3imtr4g simp11 simplrr elun1 elun2 anim12i eleq2
      cin an32 anbi12d rcla4ev sylan2 an12s rexlimdva syl9r impr sylan2b filint
      rexlimiva 3expib syl2im syland anbi12i inex1 isfild isfildu jca ) CGHZCIU
      CZAJZUBZDUDZYJBJZUNZCKZBCUEZLZACUEZMZCUBZGKDYTUBUDYSEJZYTKZEUAEDYTYSUUBUU
      ADHZUUBUUAYJKZACNZYSUUCAUUACOZYRYHUUEUUCPYIYRUUEUUCYRUUELYQUUDLZACNZUUCYQ
      UUDACUFZUUGUUCACYQYLUUAYKHZUUCUUDYLYPUGUUAYJUHYLUUJUUCYKDUUAUIUJUKULQRUMU
      OUPZYIYRDSKZYHYIYRLYQACNUULYQACUQYQUULACYLUULYPYLYKSKUULYJAURUSYKDSUTVAVB
      ULQVFZYSUUBEDTZDYTKZYSDYJKZACNZUUOYHYIYRUUQYHYRUUPACUEZYIUUQYHYQUUPACYHYJ
      CKZLZYKYJKZYQUUPUUTYJGKZUVACGYJVCZYJYKYKVDZVEQYLUVAUUPVGYPYKDYJUTVBVHVIYI
      UURUUQUUPACUQRVJVKADCOVLYSUULUUNUUOVGUUMEDYTSVMQVNZYSIYJKZVPZACUEZUUBEITZ
      VPZYHYIUVHYRYHUVGACUUTUVBUVGUVCYJVOQVQVRUVJUVFACNZVPUVHUVIUVKUVIIYTKZUVKI
      SKUVIUVLVGVSEIYTSVMVTAICOWAWBUVFACWCWDVLYSUAJZDHZUUAUVMHZMZUUEUVMYJKZACNZ
      UUBEUUATZUUBEUVMTZUVPUUEUUHUVRUVPYRUUEUUHPYHYIYRUVNUVOWEYRUUEUUHUUIRQYSYH
      UVNUVOUUHUVRPYHYIYRWGYHUVNUVOMZUUGUVQACUWAUUSUUGUVQUWAUUSUUGLZLZUVBUUDUVM
      YKHZUVOUVQUWAYHUUSUVBUWBYHUVNUVOWGUUSUUGUGUVCUKUWAUUSYQUUDWFUWCUVMDYKYHUV
      NUVOUWBWHUWBYLUWAUUSYLYPUUDWIWJWKYHUVNUVOUWBWLUVBUUDUWDUVOMUVQUUAUVMYJYKU
      VDWMWQWNWOWPWRWSUVSUUBUUEEEYTWTZUUFWAUVTUVMYTKUVRUAEYTWTAUVMCOWAZXAYSUVNU
      UCMZUVRUUAYMKZBCNZLUVMUUAXHZFJZKZFCNZUVTUVSLUUBEUWJTZUWGUVRYQUVQLZACNZUWI
      UWMUWGYRUVRUWPPYHYIYRUVNUUCWEYRUVRUWPYQUVQACUFRQUWGYHUWPUWILUVMUWKKZUUAUW
      KKZLZFCNZUWMYHYIYRUVNUUCXBUWPUWIUWTUWOUWIUWTPZACUWOUUSYLUVQLZYPLUXAYLYPUV
      QXIUUSUXBYPUXAYPUWIYOUWHLZBCNZUUSUXBLZUWTYPUWIUXDYOUWHBCUFRUXEUXCUWTBCUXE
      YMCKZLUVQUXCUWTPUUSYLUVQUXFXCUVQUXCUWTYOUVQUWHUWTUVQUWHLYOUVMYNKZUUAYNKZL
      ZUWTUVQUXGUWHUXHUVMYJYMXDUUAYMYJXEXFUWSUXIFYNCUWKYNUDUWQUXGUWRUXHUWKYNUVM
      XGUWKYNUUAXGXJXKXLXMRQXNXOXPXQXSWQYHUWSUWLFCYHUWKCKLUWKGKZUWSUWLPCGUWKVCU
      XJUWQUWRUWLUVMUUAUWKXRXTQWPYAYBUVTUVRUVSUWIUWFUVSUUBUWIUWEBUUACOWAYCUWNUW
      JYTKZUWMUWJSKUWNUXKVGUVMUUAUAURYDEUWJYTSVMVTFUWJCOWAXAYEYSUUBEDYTUUKUUMUV
      EYFYG $.
      $( [28-Nov-2013] $)
  $}


