$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c fLim $.

  $( Extend class notation with a function returning the limit of a filter. $)
  cflim $a class fLim $.

  ${
    $d x a f l $.
    $( Define a function (indexed by a topology ` x ` ) whose value is the
       limits of a filter ` a ` . $)
    df-flim $a |- fLim = ( x e. Top |->
                    ( a e. { f e. Fil | U. f = U. x } |->
                     { l e. U. x | ( ( nei ` x ) ` { l } ) C_ a } ) ) $.
  $}

  ${
    $d J a f l x $.  $d X a f l x $.
    flimfval.1 $e |- X = U. J $.
    $( Functions whose values are the limits of the filters.  (Contributed by
       FL, 1-Sep-2008.)  (Revised by Mario Carneiro, 29-Nov-2013.) $)
    flimfval $p |- ( J e. Top -> ( fLim ` J ) =
                   ( a e. { f e. Fil | U. f = X } |->
                     { l e. X | ( ( nei ` J ) ` { l } ) C_ a } ) ) $=
      ( vx ctop wcel cv cuni wceq cfil crab cnei cfv wss cmpt cvv cflim csn cpw
      wi rabss eqimss vex elpw sspwuni bitri sylibr a1i mprgbir uniexg syl5eqel
      pwexg 3syl ssexg sylancr mptexg unieq syl6eqr eqeq2d rabbidv fveq2 fveq1d
      syl sseq1d rabeqbidv mpteq12dv df-flim fvmptg mpdan ) BHIZDAJZKZCLZAMNZEJ
      UAZBOPZPZDJZQZECNZRZSIZBTPWDLVMVQSIZWEVMVQCUBZUBZQZWHSIZWFWIVPVNWHIZUCZAM
      VPAMWHUDWLVNMIVPVOCQZWKVOCUEWKVNWGQWMVNWGAUFUGVNCUHUIUJUKULVMCSIWGSIWJVMC
      BKZSFBHUMUNCSUOWGSUOUPVQWHSUQURDVQWCSUSVFGBDVOGJZKZLZAMNZVRWOOPZPZWAQZEWP
      NZRWDHSTWOBLZDWRXBVQWCXCWQVPAMXCWPCVOXCWPWNCWOBUTFVAZVBVCXCXAWBEWPCXDXCWT
      VTWAXCVRWSVSWOBOVDVEVGVHVIGADEVJVKVL $.
      $( [29-Nov-2013] $) $( [17-Aug-2008] $)
  $}

  ${
    $d a f l F $.  $d a f l J $.  $d a f l X $.  $d f Y $.
    flimval.1 $e |- X = U. J $.
    flimval.2 $e |- Y = U. F $.
    $( The set of limit points of a filter.  (Contributed by Jeff Hankins,
       4-Sep-2009.) $)
    flimval $p |- ( ( J e. Top /\ F e. Fil /\ X = Y ) -> ( ( fLim ` J ) ` F ) =
    { l e. X | ( ( nei ` J ) ` { l } ) C_ F } ) $=
      ( va vf ctop wcel cfil wceq cfv cv wss crab wa cuni cvv csn cnei flimfval
      cflim cmpt fveq1d adantr eqcom unieq syl6eqr eqeq1d elrab biimpri sylan2b
      uniexg syl5eqel rabexg syl sseq2 rabbidv eqid fvmptg syl2anr eqtrd 3impb
      ) BJKZALKZCDMZABUDNZNZEOUABUBNNZAPZECQZMVFVGVHRZRVJAHIOZSZCMZILQZVKHOZPZE
      CQZUEZNZVMVFVJWCMVNVFAVIWBIBCHEFUCUFUGVNAVRKZVMTKZWCVMMVFVHVGDCMZWDCDUHWD
      VGWFRVQWFIALVOAMZVPDCWGVPASDVOAUIGUJUKULUMUNVFCTKWEVFCBSTFBJUOUPVLECTUQUR
      HAWAVMVRTWBVSAMVTVLECVSAVKUSUTWBVAVBVCVDVE $.
      $( [29-Nov-2013] $) $( [4-Sep-2009] $)
  $}

  ${
    $d l A $.  $d l F $.  $d l J $.  $d l X $.  $d l Y $.
    elflim.1 $e |- X = U. J $.
    elflim.2 $e |- Y = U. F $.
    $( The predicate "is a limit point of a filter."  (Contributed by Jeff
       Hankins, 4-Sep-2009.) $)
    elflim $p |- ( ( J e. Top /\ F e. Fil /\ X = Y ) ->
                   ( A e. ( ( fLim ` J ) ` F ) <->
                     ( A e. X /\ ( ( nei ` J ) ` { A } ) C_ F ) ) ) $=
      ( vl ctop wcel cfil wceq w3a cflim cfv cv csn cnei wss crab eleq2d fveq2d
      wa flimval sneq sseq1d elrab syl6bb ) CIJBKJDELMZABCNOOZJAHPZQZCROZOZBSZH
      DTZJADJAQZUMOZBSZUCUIUJUPABCDEHFGUDUAUOUSHADUKALZUNURBUTULUQUMUKAUEUBUFUG
      UH $.
      $( [29-Nov-2013] $) $( [4-Sep-2009] $)
  $}

  ${
    limfilss.1 $e |- X = U. J $.
    limfilss.2 $e |- Y = U. F $.
    limfilss.3 $e |- Z = U. G $.
    $( A limit point of a filter is a limit point of a finer filter.
       (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    limfilss $p |- ( ( ( ( J e. Top /\ F e. Fil /\ G e. Fil ) /\ X = Y /\ X = Z
    ) /\ F C_ G /\ A e. ( ( fLim ` J ) ` F ) ) -> A e. ( ( fLim ` J ) ` G ) )
        $=
      ( wcel cfil w3a wceq wss cfv wa wb elflim 3expa ctop cflim csn cnei sstr2
      wi anim2d adantl 3adantl3 3adant3 adantr 3adantl2 3adant2 3imtr4d 3impia
      com12 ) DUAKZBLKZCLKZMZEFNZEGNZMZBCOZABDUBPZPKZACVEPKZVCVDQAEKZAUCDUDPPZB
      OZQZVHVICOZQZVFVGVDVKVMUFVCVDVJVLVHVJVDVLVIBCUEUPUGUHVCVFVKRZVDUTVAVNVBUQ
      URVAVNUSUQURVAVNABDEFHISTUIUJUKVCVGVMRZVDUTVBVOVAUQUSVBVOURUQUSVBVOACDEGH
      JSTULUMUKUNUO $.
      $( [5-Sep-2009] $)
  $}

  ${
    flimelbas.1 $e |- X = U. J $.
    flimelbas.2 $e |- Y = U. F $.
    $( A limit point of a filter belongs to its base set.  (Contributed by Jeff
       Hankins, 4-Sep-2009.) $)
    flimelbas $p |- ( ( ( J e. Top /\ F e. Fil /\ X = Y ) /\ A e. ( ( fLim ` J
    ) ` F ) ) -> A e. X ) $=
      ( ctop wcel cfil wceq w3a cflim cfv csn cnei wss elflim simprbda ) CHIBJI
      DEKLABCMNNIADIAOCPNNBQABCDEFGRS $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    flimnei.1 $e |- X = U. J $.
    flimnei.2 $e |- Y = U. F $.
    $( A filter contains all of the neighborhoods of its limit points.
       (Contributed by Jeff Hankins, 4-Sep-2009.) $)
    flimnei $p |- ( ( ( J e. Top /\ F e. Fil /\ X = Y ) /\ A e. ( ( fLim ` J )
    ` F ) /\ N e. ( ( nei ` J ) ` { A } ) ) -> N e. F ) $=
      ( ctop wcel cfil wceq w3a cflim cfv csn cnei wa wss elflim simplbda sseld
      3impia ) CIJBKJEFLMZABCNOOJZDAPCQOOZJDBJUDUERUFBDUDUEAEJUFBSABCEFGHTUAUBU
      C $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    $d J x y $.  $d S x y $.  $d X x y $.
    neifil.1 $e |- X = U. J $.
    $( The neighborhoods of a non empty set is a filter.  Bourbaki TG I.36,
       example 2.  (Contributed by FL, 19-Sep-2007.) $)
    neifil $p |- ( ( J e. Top /\ S C_ X /\ S =/= (/) ) ->
      ( ( nei ` J ) ` S ) e. Fil ) $=
      ( vy vx ctop wcel wss c0 wne w3a cnei cfv wa cv wi wral biimpa 3adant3 wn
      cuni wrex cpw cin cfil 0nnei 3adant2 unnei tpnei eqeltrd jca elpwi sseq2d
      sylan2 ssnei2 exp43 rexlimdv imp adantlr syldan ralrimiva 3expib 3ad2ant1
      com23 innei ralrimivv eqid isfil2 syl3anbrc ) BGHZACIZAJKZLZJABMNNZHUAZVO
      UBZVOHZOEPZFPZIZEVOUCZVTVOHZQZFVQUDZRZVTVSUEVOHZEVORFVORVOUFHVNVPVRVKVMVP
      VLABUGUHVKVLVRVMVKVLOZVQCVOABCDUIZVKVLCVOHABCDUJSUKTULVKVLWFVMWHWDFWEWHVT
      WEHZVTCIZWDWJWHVTVQIZWKVTVQUMWHWLWKWHVQCVTWIUNSUOVKWKWDVLVKWKWDVKWBWKWCVK
      WAWKWCQEVOVKVSVOHZWAWKWCABVTVSCDUPUQURVEUSUTVAVBTVNWGFEVOVOVKVLWCWMOWGQVM
      VKWCWMWGABVSVTVFVCVDVGFEVOVQVQVHVIVJ $.
      $( [28-Nov-2013] $) $( [18-Sep-2007] $)
  $}

  ${
    $d f u v w x y z J $.  $d f u v w x y z X $.
    hausflim.1 $e |- X = U. J $.
    $( A condition for a topology to be Hausdorff in terms of filters.  A
       topology is Hausdorff iff every filter has at most one limit point.
       (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    hausflim $p |- ( J e. Top -> ( J e. Haus
        <-> A. f e. Fil ( X = U. f -> E* x x e. ( ( fLim ` J ) ` f ) ) ) ) $=
      ( vu vv vy wcel wne c0 wceq cfv wi wral cfil wa syl wn adantr vz ctop cha
      vw cin csn cnei wrex cuni cflim wmo hausnei2 wal eqid flimelbas anim12dan
      cv w3a neeq1 fveq2d rexeqdv imbi12d neeq2 rexbidv rcla42v filesn 3ad2ant2
      sneq df-ne pm2.27 simp3 simpll2 flimnei adantrr adantlrr adantrl adantlrl
      3expa filint syl3anc 3adant3 3exp com3l rexlimivv syl6 com13 impr syl5bir
      eqeltrrd mt3d expr syld com23 alrimdv 3expib exp4a ralrimdv ralnex bicomi
      ex ralbii bitr3i wex cun cfi cfg cfbas wss ssun1 snssi tpnei 3ad2ant1 vex
      wb mpbid snnz neifil mp3an3 sylan2 filfbas cvv fvex mpbir2and unnei ax-mp
      ad2antrl sstri imp syl5ss elflim exp520 com24 3impib impcom anbi1d notbid
      eleq1 anbi12d eleq2d bitri ne0i ssn0 sylancr 2ralbii sylan2br jca fbunfip
      simpr mpbird unex fsubbas fgfil uneq12d unidm syl6req uniun fiuni syl5eqr
      fgbas eqtrd adantl simpll ssfii fbssfg imp3a simplr ssun2 biimpi 3ad2ant3
      mp1i ad2antlr eqeq1 anbi2d eqeq2 sylan9bb cla42ev jcai unieq eqeq2d fveq2
      syl21anc 2exbidv rcla4ev syldan expcom exanali exbii anbi2i rexbii rexnal
      exnal annim syl6ib syl5bi con4d ralrimdvv impbid mo4 imbi2i syl6bbr bitrd
      mpdd ) CUBIZCUCIUAUQZUDUQZJZFUQZGUQZUEZKLZGUXEUFZCUGMZMZUHZFUXDUFZUXLMZUH
      ZNZUDDOUADOZDBUQZUIZLZAUQZUXTCUJMZMZIZAUKZNZBPOZUAUDGFCDEULUXCUXSUYBUYFHU
      QZUYEIZQZUYCUYJLZNZHUMZAUMZNZBPOZUYIUXCUXSUYRUXCUXSUYQBPUXCUXSUXTPIZUYBUY
      PUXCUYSUYBQUXSUYPUXCUYSUYBUXSUYPNUXCUYSUYBURZUXSUYOAUYTUXSUYNHUYTUYLUXSUY
      MUYTUYLUXSUYMNUYTUYLQZUXSUYCUYJJZUXJGUYJUFZUXLMZUHZFUYCUFZUXLMZUHZNZUYMVU
      AUYCDIZUYJDIZQUXSVUINUYTUYFVUJUYKVUKUYCUXTCDUYAEUYAUNZUOUYJUXTCDUYAEVULUO
      UPUXRVUIUYCUXEJZUXNFVUGUHZNUAUDUYCUYJDDUXDUYCLZUXFVUMUXQVUNUXDUYCUXEUSVUO
      UXNFUXPVUGVUOUXOVUFUXLUXDUYCVHUTVAVBUXEUYJLZVUMVUBVUNVUHUXEUYJUYCVCVUPUXN
      VUEFVUGVUPUXJGUXMVUDVUPUXKVUCUXLUXEUYJVHUTVAVDVBVERUYTUYLVUIUYMUYTUYLVUIQ
      ZQZUYMKUXTIZUYTVUSSZVUQUYSUXCVUTUYBUXTVFVGTUYMSZVUBVURVUSUYCUYJVIUYTUYLVU
      IVUBVUSNVUBVUIVUAVUSVUBVUIVUHVUAVUSNZVUBVUHVJUXJVVBFGVUGVUDVUAUXGVUGIZUXH
      VUDIZQZUXJVUSVUAVVEUXJVUSVUAVVEUXJURUXIKUXTVUAVVEUXJVKVUAVVEUXIUXTIZUXJVU
      AVVEQUYSUXGUXTIZUXHUXTIZVVFUXCUYSUYBUYLVVEVLUYTUYFVVEVVGUYKUYTUYFQVVCVVGV
      VDUYTUYFVVCVVGUYCUXTCUXGDUYAEVULVMVRVNVOUYTUYKVVEVVHUYFUYTUYKQVVDVVHVVCUY
      TUYKVVDVVHUYJUXTCUXHDUYAEVULVMVRVPVQUXGUXHUXTVSVTWAWIWBWCWDWEWFWGWHWJWKWL
      WTWMWNWNWOWMWPWQUXCUYRUXRUAUDDDUXCUXFUXDDIZUXEDIZQZUYRUXQUXCVVKUXFUYRUXQN
      ZUXCVVKUXFVVLUXCVVKUXFURZUXQUYRUXQSZUXJSZGUXMOZFUXPOZVVMUYRSZVVNUXNSZFUXP
      OVVQUXNFUXPWRVVSVVPFUXPVVPVVSUXJGUXMWRWSXAXBVVMVVQUYBUYLVVAQZHXCZAXCZQZBP
      UHZVVRVVMVVQUXPUXMXDZXEMZXFMZPIZVWDVVMVVQVWHVVMVVQQZVWFXGIZVWHVWIVWJVWEKJ
      ZKVWFISZVWIUXPVWEXHUXPKJZVWKUXPUXMXIZVVMVWMVVQVVMDUXPIZVWMVVMUXODXHZVWOVV
      KUXCVWPUXFVVIVWPVVJUXDDXJZTVGUXCVVKVWPVWOXNUXFUXOCDEXKXLXOUXPDUUARTUXPVWE
      UUBUUCVWIVWLUXIKJZGUXMOFUXPOZVVQVVMVWSVWSVWRVVOFGUXPUXMUXIKVIUUDVVMVWSUUH
      UUEVVMVWLVWSXNZVVQVVMUXPXGIZUXMXGIZQZVWTUXCVVKVXCUXFUXCVVKQZVXAVXBVXDUXPP
      IZVXAUXCVVIVXEVVJVVIUXCVWPVXEVWQUXCVWPUXOKJVXEUXDUAXMZXPUXOCDEXQXRXSVNUXP
      XTRUXCVVJVXBVVIUXCVVJQUXMPIZVXBVVJUXCUXKDXHZVXGUXEDXJZUXCVXHUXKKJVXGUXEUD
      XMZXPUXKCDEXQXRXSUXMXTRVPUUFWAFGUXPUXMUUGRTUUIVWEYAIZVWJVWKVWLQXNVWIUXPUX
      MUXOUXLYBUXKUXLYBUUJZVWEYAUUKUVJYCZVWFUULRWTVVMVVQVWHVWDNVWHVWIVWDVWHVWID
      VWGUIZLZUYCVWGUYDMZIZUYJVXPIZQZVVAQZHXCAXCZQZVWDVWHVWIQZVXOVYAVWIVXOVWHVW
      IDUXPUIZUXMUIZXDZVXNVVMDVYFLZVVQUXCVVKVYGUXFVXDVYFDDXDDVXDVYDDVYEDUXCVVIV
      YDDLZVVJVVIUXCVWPVYHVWQUXOCDEYDXSVNUXCVVJVYEDLZVVIVVJUXCVXHVYIVXIUXKCDEYD
      XSVPUUMDUUNUUOWATVWIVWJVYFVXNLVXMVWFVYFVXKVYFVWFUIZLVXLVXKVYFVWEUIVYJUXPU
      XMUUPVWEYAUUQUURYEUUSRUUTUVAVYCVXOVYAVYCVXOQUXDVXPIZUXEVXPIZUXDUXELZSZVYA
      VYCVXOVYKVWIVWHVXOVYKNZVVMVVQVWHVYONVVMVXOVWHVVQVYKVVMVWHVXOVVQVYKNZUXCVV
      KUXFVWHVXOVYPNNUXCVXOVWHVVKUXFQZVYPUXCVWHVXOVYQVYPNUXCVWHVXOVYQVVQVYKUXCV
      WHVXOURZVYQVVQQZQZVYKVVIUXPVWGXHZVYQVVIVYRVVQVVIVVJUXFUVBYFVYTUXPVWFVWGUX
      PVWEVWFVWNVXKVWEVWFXHVXLVWEYAUVCYEZYGVYRVYSVWFVWGXHZUXCVWHVYSWUCNVXOUXCVY
      QVVQWUCUXCVVKUXFVVQWUCNVVMVVQWUCVWIVWJWUCVXMVWFUVDRWTWOUVEXLYHZYIVYRVYKVV
      IWUAQXNVYSUXDVWGCDVXNEVXNUNZYJTYCYKWMYLYMWMYLYHYNYHVYCVXOVYLVWIVWHVXOVYLN
      ZVVMVVQVWHWUFNVVMVXOVWHVVQVYLVVMVWHVXOVVQVYLNZUXCVVKUXFVWHVXOWUGNNUXCVXOV
      WHVYQWUGUXCVWHVXOVYQWUGNUXCVWHVXOVYQVVQVYLVYTVYLVVJUXMVWGXHZVYQVVJVYRVVQV
      VIVVJUXFUVFYFVYTUXMVWFVWGUXMVWEVWFUXMUXPUVGWUBYGWUDYIVYRVYLVVJWUHQXNVYSUX
      EVWGCDVXNEWUEYJTYCYKWMYLYMWMYLYHYNYHVWIVYNVWHVXOVVMVYNVVQUXFUXCVYNVVKUXFV
      YNUXDUXEVIUVHUVITUVKVXTVYKVYLQZVYNQZAHUXDUXEVXFVXJUYCUXDLZVXTVYKVXRQZUXDU
      YJLZSZQUYJUXELZWUJWUKVXSWULVVAWUNWUKVXQVYKVXRUYCUXDVXPYQYOWUKUYMWUMUYCUXD
      UYJUVLYPYRWUOWULWUIWUNVYNWUOVXRVYLVYKUYJUXEVXPYQUVMWUOWUMVYMUYJUXEUXDUVNY
      PYRUVOUVPUWAWTUVQVWCVYBBVWGPUXTVWGLZUYBVXOVWBVYAWUPUYAVXNDUXTVWGUVRUVSWUP
      VVTVXTAHWUPUYLVXSVVAWUPUYFVXQUYKVXRWUPUYEVXPUYCUXTVWGUYDUVTZYSWUPUYEVXPUY
      JWUQYSYRYOUWBYRUWCUWDUWEWTUXBVWDUYQSZBPUHVVRVWCWURBPVWCUYBUYPSZQWURVWBWUS
      UYBVWBUYOSZAXCWUSVWAWUTAUYLUYMHUWFUWGUYOAUWKYTUWHUYBUYPUWLYTUWIUYQBPUWJYT
      UWMUWNUWOWBWMYLUWPUWQUYHUYQBPUYGUYPUYBUYFUYKAHUYCUYJUYEYQUWRUWSXAUWTUXA
      $.
      $( [28-Nov-2013] $) $( [5-Sep-2009] $)
  $}


  $c FilMap $.
  $( Extend class definition to include the neighborhood filter mapping
     function. $)
  cfm $a class FilMap $.

  $c fLimf $.
  $( Extend class definition to include the function for filter-based function
     limits. $)
  cflf $a class fLimf $.

  ${
    $d f t x y z $.
    $( Define a function that takes a filter to a neighborhood filter of the
       range.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    df-fm $a |- FilMap = ( x e. _V , y e. fBas |->
                     ( f e. ( x ^m U. y ) |-> ( filGen `
                       ( { z | E. t e. y z = ( f " t ) } u. { x } ) ) ) ) $.

    $( Define a function that gives the limits of a function ` f ` in the
       filter sense.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    df-flf $a |- fLimf = ( x e. Top , y e. Fil |-> ( f e. ( U. x ^m U. y ) |->
                     ( ( fLim ` x ) ` ( ( U. x FilMap y ) ` f ) ) ) ) $.
  $}

  ${
    $d f x y F $.  $d f w x y z B $.  $d f w x y z X $.  $d f w y z Y $.
    fmfval.1 $e |- Y = U. B $.
    $( Given a function from a filtered set to a topology, return the filter of
       supersets of images of filter elements.  (Contributed by Jeff Hankins,
       5-Sep-2009.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    fmfval $p |- ( ( X e. V /\ B e. fBas ) -> ( X FilMap B ) =
                       ( f e. ( X ^m Y ) |-> ( filGen `
                         ( { x | E. y e. B x = ( f " y ) } u. { X } ) ) ) ) $=
      ( vz vw wcel cvv co cmap cv wceq cun cfg cfv cmpt cfbas cfm cima wrex cab
      elex cuni oveq1 uneq2d fveq2d mpteq12dv unieq syl6eqr oveq2d rexeq abbidv
      csn sneq uneq1d df-fm ovex mptex ovmpt2 sylan ) FEKFLKCUAKFCUBMDFGNMZAODO
      BOUCPZBCUDZAUEZFUQZQZRSZTZPFEUFIJFCLUADIOZJOZUGZNMZVFBVNUDZAUEZVMUQZQZRSZ
      TVLUBDFVONMZVRVIQZRSZTVMFPZDVPWAWBWDVMFVONUHWEVTWCRWEVSVIVRVMFURUIUJUKVNC
      PZDWBWDVEVKWFVOGFNWFVOCUGGVNCULHUMUNWFWCVJRWFVRVHVIWFVQVGAVFBVNCUOUPUSUJU
      KIJABDUTDVEVKFGNVAVBVCVD $.
      $( [15-Dec-2013] $) $( [5-Sep-2009] $)

    $( Introduce a function that takes a function from a filtered domain to a
       set and produces a filter which consists of supersets of images of
       filter elements.  The functions which are dealt with by this function
       are similar to nets in topology.  For example, suppose we have a
       sequence filtered by the filter generated by its tails under the usual
       natural number ordering.  Then the elements of this filter are precisely
       the supersets of tails of this sequence.  Under this definition, it is
       not too difficult to see that the limit of a function in the filter
       sense captures the notion of convergence of a sequence.  As a result,
       the notion of a filter generalizes many ideas associated with sequences,
       and this function is one way to make that relationship precise in
       Metamath.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    fmval $p |- ( ( X e. A /\ B e. fBas /\ F : Y --> X ) ->
                      ( ( X FilMap B ) ` F ) = ( filGen `
                        ( { x | E. y e. B x = ( F " y ) } u. { X } ) ) ) $=
      ( vf wcel cfbas co cfv cv cima wceq wrex cab cfg cvv wf cfm csn cun wa wb
      cmap cuni uniexg syl5eqel elmapg sylan2 biimpar cmpt fmfval fveq1d imaeq1
      eqeq2d rexbidv abbidv uneq1d fveq2d eqid fvex fvmpt sylan9eq syldan 3impa
      ) FCJZDKJZGFEUAZEFDUBLZMZANZEBNZOZPZBDQZARZFUCZUDZSMZPZVIVJUEZVKEFGUGLZJZ
      WCWDWFVKVJVIGTJWFVKUFVJGDUHTHDKUIUJFGECTUKULUMWDWFVMEIWEVNINZVOOZPZBDQZAR
      ZVTUDZSMZUNZMWBWDEVLWNABDICFGHUOUPIEWMWBWEWNWGEPZWLWASWOWKVSVTWOWJVRAWOWI
      VQBDWOWHVPVNWGEVOUQURUSUTVAVBWNVCWASVDVEVFVGVH $.
      $( [15-Dec-2013] $) $( [5-Sep-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z F $.  $d x y z X $.
    $d x y z Y $.  $d x y z Z $.
    fmss.1 $e |- Y = U. B $.
    fmss.2 $e |- Z = U. C $.
    $( A finer filter produces a finer image filter.  (Contributed by Jeff
       Hankins, 16-Nov-2009.) $)
    fmss $p |- ( ( ( X e. A /\ B e. fBas /\ C e. fBas ) /\ ( F : Y --> X /\
    Y = Z /\ B C_ C ) ) -> ( ( X FilMap B ) ` F ) C_ ( ( X FilMap C ) ` F ) )
        $=
      ( vx vy vz wcel cfbas wceq wss wrex cfv eqid adantl w3a wf wa cv cima cab
      csn cun cfg cfm cuni wfn simp2 ffn 3ad2ant1 filrn syl2an wral vex rexbidv
      co eqeq1 elab crn imassrn frn syl5ss syl5ibrcom rexlimdvw syl5bi ralrimiv
      sseq1 unissb sylibr extbas1 syl2anc simp3 fneq2 biimpac 3adant3 wi ssrexv
      sylan 3ad2ant3 ss2abdv unss1 syl fgss syl3anc fmval 3expa 3adantl3 simpl1
      3ad2antr1 simpl3 feq2 3sstr4d ) EAMZBNMZCNMZUAZFEDUBZFGOZBCPZUAZUCZJUDZDK
      UDZUEZOZKBQZJUFZEUGZUHZUIRZXJKCQZJUFZXMUHZUIRZDEBUJVARZDECUJVARZXFXNNMZXR
      NMZXNXRPZXOXSPXFXLNMZXLUKZEPZYBXAWSDFULZYEXEWRWSWTUMXBXCYHXDFEDUNZUOKJBXL
      DFHXLSUPUQXEYGXAXBXCYGXDXBLUDZEPZLXLURYGXBYKLXLYJXLMYJXIOZKBQZXBYKXKYMJYJ
      LUSZXGYJOZXJYLKBXGYJXIVBZUTVCXBYLYKKBXBYKYLXIEPXBXIDVDEDXHVEFEDVFVGYJXIEV
      LVHZVIVJVKLXLEVMVNUOTEXLYFYFSVOVPXFXQNMZXQUKZEPZYCXAWTDGULZYRXEWRWSWTVQXB
      XCUUAXDXBYHXCUUAYIXCYHUUAFGDVRVSWCVTKJCXQDGIXQSUPUQXEYTXAXBXCYTXDXBYKLXQU
      RYTXBYKLXQYJXQMYLKCQZXBYKXPUUBJYJYNYOXJYLKCYPUTVCXBYLYKKCYQVIVJVKLXQEVMVN
      UOTEXQYSYSSVOVPXFXLXQPYDXFXKXPJXEXKXPWAZXAXDXBUUCXCXJKBCWBWDTWEXLXQXMWFWG
      XNXRWHWIWRWSXEXTXOOZWTWRWSUCXCXBUUDXDWRWSXBUUDJKABDEFHWJWKWNWLXFWRWTGEDUB
      ZYAXSOWRWSWTXEWMWRWSWTXEWOXEUUEXAXBXCUUEXDXCXBUUEFGEDWPVSVTTJKACDEGIWJWIW
      Q $.
      $( [16-Nov-2009] $)
  $}

  ${
    $d t w A $.  $d t w B $.  $d t w F $.  $d t w X $.  $d t w Y $.
    fmfil.1 $e |- Y = U. B $.
    $( A mapping filter is a filter.  (Contributed by Jeff Hankins,
       18-Sep-2009.) $)
    fmfil $p |- ( ( X e. A /\ B e. fBas /\ F : Y --> X ) -> ( ( X FilMap B ) `
        F
    ) e. Fil ) $=
      ( vw vt wcel cfbas cfv cv wceq cfil wss eqid cvv wral syl 3ad2ant3 wf w3a
      cfm co cima wrex cab csn cun cfg fmval cuni wfn filrn sylan2 3adant1 ciun
      ffn wfun ffun vex funimaex ralrimivw dfiun2g crn imassrn frn syl5ss iunss
      sylibr eqsstr3d extbas1 syl2anc fgfil eqeltrd ) DAIZBJIZEDCUAZUBZCDBUCUDK
      GLCHLZUEZMHBUFGUGZDUHUIZUJKZNGHABCDEFUKVSWCJIZWDNIVSWBJIZWBULZDOWEVQVRWFV
      PVRVQCEUMWFEDCURHGBWBCEFWBPUNUOUPVSWGHBWAUQZDVSWAQIZHBRWHWGMVSWIHBVRVPWIV
      QVRCUSWIEDCUTCVTHVAVBSTVCHGBWAQVDSVSWADOZHBRZWHDOVRVPWKVQVRWJHBVRWACVEDCV
      TVFEDCVGVHVCTHBWADVIVJVKDWBWGWGPVLVMWCVNSVO $.
      $( [1-Dec-2010] $) $( [18-Sep-2009] $)
  $}

  ${
    $d t w A $.  $d t w B $.  $d t w F $.  $d t w X $.  $d t w Y $.
    fmbas.1 $e |- Y = U. B $.
    $( The base set of a mapping filter is the first argument.  (Contributed by
       Jeff Hankins, 18-Sep-2009.) $)
    fmbas $p |- ( ( X e. A /\ B e. fBas /\ F : Y --> X ) -> U. ( ( X FilMap B )
    ` F ) = X ) $=
      ( vw vt wcel cfbas cfv cuni cv wceq wss eqid cvv wral syl ralrimivw wf co
      w3a cfm cima wrex cab csn cun cfg fmval unieqd wfn ffn filrn 3adant1 ciun
      sylan2 wfun vex funimaex 3ad2ant3 dfiun2g crn imassrn syl5ss iunss sylibr
      ffun frn eqsstr3d extbas1 syl2anc fgbas simp1 extbas2 3eqtr2d ) DAIZBJIZE
      DCUAZUCZCDBUDUBKZLGMCHMZUEZNHBUFGUGZDUHUIZUJKZLZWFLZDWAWBWGGHABCDEFUKULWA
      WFJIZWIWHNWAWEJIZWELZDOZWJVSVTWKVRVTVSCEUMWKEDCUNHGBWECEFWEPUOURUPWAWLHBW
      DUQZDWAWDQIZHBRZWNWLNVTVRWPVSVTWOHBVTCUSWOEDCVICWCHUTVASTVBHGBWDQVCSWAWDD
      OZHBRZWNDOVTVRWRVSVTWQHBVTWDCVDDCWCVEEDCVJVFTVBHBWDDVGVHVKZDWEWLWLPZVLVMW
      FWIWIPVNSWAWMVRWIDNWSVRVSVTVODAWEWLWTVPVMVQ $.
      $( [1-Dec-2010] $) $( [18-Sep-2009] $)
  $}

  ${
    $d t w x y z A $.  $d t w x y z B $.  $d t w x y C $.  $d t w x y z F $.
    $d x y z L $.  $d t w x y z X $.  $d t w x y z Y $.
    elfm.1 $e |- Y = U. B $.
    $( An element of a mapping filter.  (Contributed by Jeff Hankins,
       8-Sep-2009.) $)
    elfm $p |- ( ( X e. C /\ B e. fBas /\ F : Y --> X ) -> ( A e. ( ( X
    FilMap B ) ` F ) <-> ( A C_ X /\ E. x e. B ( F " x ) C_ A ) ) ) $=
      ( vw vt vy wcel cv wceq wrex wss wa 3ad2ant3 syl2anc wi cfbas w3a cfm cfv
      wf co cima cab csn cun cfg cuni fmval eleq2d wb wfn ffn eqid filrn sylan2
      3adant1 ciun cvv wral wfun vex funimaex syl ralrimivw dfiun2g crn imassrn
      ffun frn syl5ss iunss sylibr eqsstr3d extbas1 elfg extbas2 sseq2d wo elun
      simp1 eqeq1 rexbidv elab orbi12i bitri simpl2 sseq1 biimpa imaeq2 rcla4ev
      elsn sseq1d 3exp1 rexlimdv wex c0 wne fbasne0 sylib adantr sstr2 3ad2ant2
      n0 sylbid imp a1i exlimdv 3imp syl3an2 syl5bi ssun1 eqeq2d mpan2 ad2antrl
      jaod elabg 3syl mpbird sseldi simprr exp32 impbid anbi12d 3bitrd ) FDLZCU
      ALZGFEUEZUBZBEFCUCUFUDZLBIMZEJMZUGZNZJCOZIUHZFUIZUJZUKUDZLZBUUBULZPZKMZBP
      ZKUUBOZQZBFPZEAMZUGZBPZACOZQYMYNUUCBIJDCEFGHUMUNYMUUBUALZUUDUUJUOYMYTUALZ
      YTULZFPZUUPYKYLUUQYJYLYKEGUPUUQGFEUQJICYTEGHYTURUSUTVAYMUURJCYQVBZFYMYQVC
      LZJCVDUUTUURNYMUVAJCYLYJUVAYKYLEVEZUVAGFEVMZEYPJVFVGVHRVIJICYQVCVJVHYMYQF
      PZJCVDZUUTFPYLYJUVEYKYLUVDJCYLYQEVKFEYPVLGFEVNVOZVIRJCYQFVPVQVRZFYTUURUUR
      URZVSSKBUUBUUEUUEURVTVHYMUUFUUKUUIUUOYMUUEFBYMUUSYJUUEFNUVGYJYKYLWEFDYTUU
      RUVHWASWBYMUUIUUOYMUUHUUOKUUBUUGUUBLZUUGYQNZJCOZUUGFNZWCZYMUUHUUOTZUVIUUG
      YTLZUUGUUALZWCUVMUUGYTUUAWDUVOUVKUVPUVLYSUVKIUUGKVFYOUUGNYRUVJJCYOUUGYQWF
      WGWHKFWPWIWJYMUVKUVNUVLYMUVJUVNJCYLYJYPCLZUVJUVNTTYKYLUVQUVJUUHUUOYLUVQUV
      JUBZUUHQUVQYQBPZUUOYLUVQUVJUUHWKUVRUUHUVSUVJYLUUHUVSUOUVQUUGYQBWLRWMUUNUV
      SAYPCUULYPNUUMYQBUULYPEWNWQWOZSWRRWSYKYJUVQJWTZYLUVLUVNTZYKCXAXBUWACXCJCX
      HXDYJUWAYLUWBYJUVQYLUWBTZJUVQUWCTYJUVQYLUVLUUHUUOUVQYLUVLUBZUUHQUVQUVSUUO
      UWDUVQUUHUVQYLUVLWEXEUWDUUHUVSUWDUUHFBPZUVSUVLUVQUUHUWEUOYLUUGFBWLRYLUVQU
      WEUVSTZUVLYLUVDUWFUVFYQFBXFVHXGXIXJUVTSWRXKXLXMXNXTXOWSYMUUNUUIACYMUULCLZ
      UUNUUIYMUWGUUNQZQZUUMUUBLUUNUUIUWIYTUUBUUMYTUUAXPUWIUUMYTLZUUMYQNZJCOZUWG
      UWLYMUUNUWGUUMUUMNZUWLUUMURUWKUWMJUULCYPUULNYQUUMUUMYPUULEWNXQWOXRXSYMUWJ
      UWLUOZUWHYLYJUWNYKYLUVBUUMVCLUWNUVCEUULAVFVGYSUWLIUUMVCYOUUMNYRUWKJCYOUUM
      YQWFWGYAYBRXEYCYDYMUWGUUNYEUUHUUNKUUMUUBUUGUUMBWLWOSYFWSYGYHYI $.
      $( [1-Dec-2010] $) $( [8-Sep-2009] $)

    elfm2.2 $e |- L = ( filGen ` B ) $.
    $( An element of a mapping filter.  (Contributed by Jeff Hankins,
       26-Sep-2009.) $)
    elfm2 $p |- ( ( X e. C /\ B e. fBas /\ F : Y --> X ) -> ( A e. ( ( X
    FilMap B ) ` F ) <-> ( A C_ X /\ E. x e. L ( F " x ) C_ A ) ) ) $=
      ( vy wcel cfv wss cv cima wrex wa rexlimdv wi cfbas wf w3a co elfm fbssfg
      cfm syl6sseqr sselda adantrr 3ad2antl2 simprr wceq imaeq2 rcla4ev syl2anc
      cfg sseq1d exp32 wb eleq2i elfg syl5bb 3ad2ant2 sstr2 com12 ad2antll syl5
      imass2 reximdv expr com23 expimpd sylbid impbid anbi2d bitrd ) GDLZCUALZH
      GEUBZUCZBEGCUGUDMLBGNZEKOZPZBNZKCQZRWBEAOZPZBNZAFQZRKBCDEGHIUEWAWFWJWBWAW
      FWJWAWEWJKCWAWCCLZWEWJWAWKWERZRWCFLZWEWJVSVRWLWMVTVSWKWMWEVSCFWCVSCCUQMZF
      CUFJUHUIUJUKWAWKWEULWIWEAWCFWGWCUMWHWDBWGWCEUNURUOUPUSSWAWIWFAFWAWGFLZWGH
      NZWCWGNZKCQZRZWIWFTZVSVRWOWSUTVTWOWGWNLVSWSFWNWGJVAKWGCHIVBVCVDWAWPWRWTWA
      WPRWIWRWFWAWPWIWRWFTWAWPWIRRZWQWEKCWQWDWHNZXAWEWCWGEVIWIXBWETWAWPXBWIWEWD
      WHBVEVFVGVHVJVKVLVMVNSVOVPVQ $.
      $( [1-Dec-2010] $) $( [26-Sep-2009] $)

    $( An alternate formulation of elementhood in a mapping filter that
       requires ` F ` to be onto.  (Contributed by Jeff Hankins,
       1-Oct-2009.) $)
    elfm3 $p |- ( ( B e. fBas /\ F : Y -onto-> X ) -> ( A e. ( ( X FilMap B
    ) ` F ) <-> E. x e. L A = ( F " x ) ) ) $=
      ( vy vz wcel cvv cv cima wceq wb wa wss adantr cfbas wfo cfm co cfv foima
      wrex adantl wfun fofun cuni uniexg syl5eqel funimaexg syl2anr eqeltrrd wf
      w3a fof elfm2 syl3an3 ccnv cfil cfg 3ad2ant2 ad2antrr simprl cdm cnvimass
      fgfil wfn fofn syl syl5sseq fgbas unieqi syl6eqr sseqtrd 3adant1 3ad2ant3
      fndm eleq2i elfg simprbda sseq2 biimpar sylan 3ad2antl3 adantlr funimass3
      syl5bb syldan syl2anc biimpd impr 3jca eqid fillsb foimacnv eqcomd imaeq2
      sylc eqeq2d rcla4ev exp32 rexlimdv expimpd simprr imassrn eqsstrd eqimss2
      crn forn sseq1d sylan2 jca impbid bitrd 3coml mpd3an3 ) CUALZGFDUBZFMLZBD
      FCUCUDUELZBDANZOZPZAEUGZQZYAYBRZDGOZFMYBYKFPYAGFDUFUHYBDUIZGMLYKMLYAGFDUJ
      ZYAGCUKMHCUAULUMDGMUNUOUPYCYAYBYIYCYAYBURZYDBFSZDJNZOZBSZJEUGZRZYHYBYCYAG
      FDUQYDYTQGFDUSJBCMDEFGHIUTVAYNYTYHYNYOYSYHYNYORZYRYHJEUUAYPELZYRYHUUAUUBY
      RRZRZDVBBOZELZBDUUEOZPZYHUUDEVCLZUUBUUEEUKZSZYPUUESZURUUFYNUUIYOUUCYAYCUU
      IYBYAECVDUEZVCICVJUMVEVFUUDUUBUUKUULUUAUUBYRVGYNUUKYOUUCYAYBUUKYCYJUUEGUU
      JYBUUEGSYAYBDVHZUUEGDBVIYBDGVKUUNGPZGFDVLGDWAVMZVNUHYJGUUMUKZUUJYAGUUQPYB
      CGHVOTEUUMIVPVQVRVSVFUUAUUBYRUULUUAUUBRZYRUULUURYLYPUUNSZYRUULQYNYLYOUUBY
      BYCYLYAYMVTVFUUAUUBYPGSZUUSUUAUUBUUTKNYPSKCUGZUUBYPUUMLZUUAUUTUVARZEUUMYP
      IWBYNUVBUVCQZYOYAYCUVDYBKYPCGHWCVETWKWDYNUUTUUSYOYBYCUUTUUSYAYBUUOUUTUUSU
      UPUUOUUSUUTUUNGYPWEWFWGWHWIWLYPBDWJWMWNWOWPYPUUEEUUJUUJWQWRXBUUAUUHUUCYBY
      CYOUUHYAYBYORUUGBGFBDWSWTWHTYGUUHAUUEEYEUUEPYFUUGBYEUUEDXAXCXDWMXEXFXGYNY
      GYTAEYNYEELZYGYTYNUVEYGRZRZYOYSUVGBYFFYNUVEYGXHUVGDXLZYFFDYEXIYNUVHFPZUVF
      YBYCUVIYAGFDXMVTTVNXJUVFYSYNYGUVEYFBSZYSYFBXKYRUVJJYEEYPYEPYQYFBYPYEDXAXN
      XDXOUHXPXEXFXQXRXSXT $.
      $( [1-Dec-2010] $) $( [1-Oct-2009] $)
  $}

  ${
    $d s x B $.  $d s x C $.  $d s x F $.  $d s x L $.  $d s x X $.
    $d s x Y $.
    fbfgfmeq.1 $e |- Y = U. B $.
    fbfgfmeq.2 $e |- L = ( filGen ` B ) $.
    $( The image filter of a filter base is the same as the image filter of its
       generated filter.  (Contributed by Jeff Hankins, 18-Nov-2009.) $)
    fbfgfmeq $p |- ( ( X e. C /\ B e. fBas /\ F : Y --> X ) -> ( ( X FilMap B )
    ` F ) = ( ( X FilMap L ) ` F ) ) $=
      ( vx vs wcel cfbas wf cfm co cfv cv wss cuni wb w3a cima wrex elfm2 simp1
      wa cfg cfil fgfil syl5eqel filfbas syl 3ad2ant2 fgbas feq2d adantl unieqi
      biimp3a eqcomi elfm syl3anc bitr4d eqrdv ) EBKZALKZFECMZUAZICEANOPZCEDNOP
      ZVGIQZVHKVJERCJQUBVJRJDUCUFZVJVIKZJVJABCDEFGHUDVGVDDLKZAUGPZSZECMZVLVKTVD
      VEVFUEVEVDVMVFVEDUHKVMVEDVNUHHAUIUJDUKULUMVDVEVFVPVEVFVPTVDVEFVOECAFGUNUO
      UPURJVJDBCEVODSVODVNHUQUSUTVAVBVC $.
      $( [18-Nov-2009] $)
  $}

  ${
    $d f x y J $.  $d f F $.  $d f x y X $.  $d f x y Y $.  $d f x y L $.
    flffval.1 $e |- X = U. J $.
    flffval.2 $e |- Y = U. L $.
    $( Given a topology and a filtered set, return the convergence function on
       the functions from the filtered set to the base set of the topological
       space.  (Contributed by Jeff Hankins, 14-Oct-2009.)  (Revised by Mario
       Carneiro, 15-Dec-2013.) $)
    flffval $p |- ( ( J e. Top /\ L e. Fil ) -> ( J fLimf L ) =
        ( f e. ( X ^m Y ) |-> ( ( fLim ` J ) ` ( ( X FilMap L ) ` f ) ) ) ) $=
      ( vx vy cv cuni cmap co cfm cfv cflim cmpt wceq unieq syl6eqr ctop oveq1d
      cfil cflf fveq2 fveq1d fveq12d mpteq12dv oveq2d oveq2 fveq2d df-flf mptex
      ovex ovmpt2 ) HIBCUAUCAHJZKZIJZKZLMZAJZUQURNMZOZUPPOZOZQADELMZVADCNMZOZBP
      OZOZQUDADUSLMZVADURNMZOZVIOZQUPBRZAUTVEVKVNVOUQDUSLVOUQBKDUPBSFTZUBVOVCVM
      VDVIUPBPUEVOVAVBVLVOUQDURNVPUBUFUGUHURCRZAVKVNVFVJVQUSEDLVQUSCKEURCSGTUIV
      QVMVHVIVQVAVLVGURCDNUJUFUKUHHIAULAVFVJDELUNUMUO $.
      $( [15-Dec-2013] $) $( [14-Oct-2009] $)

    $( Given a function from a filtered set to a topological space, define the
       set of limit points of the function.  (Contributed by Jeff Hankins,
       8-Nov-2009.) $)
    flfval $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) ->
      ( ( J fLimf L ) ` F ) = ( ( fLim ` J ) ` ( ( X FilMap L ) ` F ) ) ) $=
      ( vf ctop wcel cfil wf cflf co cfv cfm cflim wceq wa cmap wb biimpar cmpt
      topopn filusb elmapg syl2an cv flffval fveq1d fveq2 fveq2d fvmpt sylan9eq
      eqid fvex syldan 3impa ) BIJZCKJZEDALZABCMNZOZADCPNZOZBQOZOZRZUSUTSZVAADE
      TNZJZVHVIVKVAUSDBJECJVKVAUAUTBDFUDCEGUEDEABCUFUGUBVIVKVCAHVJHUHZVDOZVFOZU
      CZOVGVIAVBVOHBCDEFGUIUJHAVNVGVJVOVLARVMVEVFVLAVDUKULVOUOVEVFUPUMUNUQUR $.
      $( [15-Dec-2013] $) $( [8-Nov-2009] $)
  $}

  ${
    $d n A $.  $d n s F $.  $d n J $.  $d n s L $.  $d n s X $.  $d n s Y $.
    flimfnei.1 $e |- X = U. J $.
    flimfnei.2 $e |- Y = U. L $.
    $( The property of being a limit point of a function in terms of
       neighborhoods.  (Contributed by Jeff Hankins, 9-Nov-2009.) $)
    flimfnei $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) -> ( A e. ( ( J
    fLimf L ) ` F ) <-> ( A e. X /\ A. n e. ( ( nei ` J ) ` { A } ) E. s e. L (
    F " s ) C_ n ) ) ) $=
      ( ctop wcel cfil co cfv wss wa wb cvv syl3anc w3a cflf cfm cflim csn cnei
      wf cima wrex wral flfval eleq2d cuni simp1 cfbas uniexg syl5eqel 3ad2ant1
      cv wceq filfbas 3ad2ant2 simp3 fmfil fmbas eqcomd eqid elflim dfss3 neii1
      3ad2antl1 id elfm syl3an adantr ibar adantl bitr4d syldan ralbidva syl5bb
      anbi2d 3bitrd ) DKLZEMLZGFCUGZUAZACDEUBNOZLACFEUCNOZDUDOOZLZAFLZAUEZDUFOO
      ZWIPZQZWLCHUSUHBUSZPHEUIZBWNUJZQWGWHWJACDEFGIJUKULWGWDWIMLZFWIUMZUTWKWPRW
      DWEWFUNWGFSLZEUOLZWFWTWDWEXBWFWDFDUMSIDKUPUQZURZWEWDXCWFEVAZVBZWDWEWFVCZS
      ECFGJVDTWGXAFWGXBXCWFXAFUTXEXGXHSECFGJVETVFAWIDFXAIXAVGVHTWGWOWSWLWOWQWIL
      ZBWNUJWGWSBWNWIVIWGXIWRBWNWGWQWNLZWQFPZXIWRRWDWEXJXKWFWMDWQFIVJVKWGXKQXIX
      KWRQZWRWGXIXLRZXKWDXBWEXCWFWFXMXDXFWFVLHWQESCFGJVMVNVOXKWRXLRWGXKWRVPVQVR
      VSVTWAWBWC $.
      $( [1-Dec-2010] $) $( [9-Nov-2009] $)
  $}

  ${
    $d n A $.  $d n s F $.  $d n J $.  $d n s L $.  $d n s N $.  $d n s X $.
    $d n s Y $.
    flimfneii.1 $e |- X = U. J $.
    flimfneii.2 $e |- Y = U. L $.
    $( A neighborhood of a limit point of a function contains the image of a
       filter element.  (Contributed by Jeff Hankins, 11-Nov-2009.) $)
    flimfneii $p |- ( ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) /\ A e. ( ( J
    fLimf L ) ` F ) /\ N e. ( ( nei ` J ) ` { A } ) ) -> E. s e. L ( F " s ) C_
    N ) $=
      ( vn ctop wcel cfil wf w3a cfv cv wss wrex cflf co csn cnei cima flimfnei
      wral simplbda 3adant3 wi wceq sseq2 rexbidv rcla4v 3ad2ant3 mpd ) CLMDNMG
      FBOPZABCDUAUBQMZEAUCCUDQQZMZPBHRUEZKRZSZHDTZKUSUGZVAESZHDTZUQURVEUTUQURAF
      MVEAKBCDFGHIJUFUHUIUTUQVEVGUJURVDVGKEUSVBEUKVCVFHDVBEVAULUMUNUOUP $.
      $( [1-Dec-2010] $) $( [11-Nov-2009] $)
  $}

  ${
    $d n o s A $.  $d n o s F $.  $d n o s J $.  $d n o s X $.  $d n o s Y $.
    flimopn.1 $e |- X = U. J $.
    flimopn.2 $e |- Y = U. F $.
    $( The condition for being a limit point of a filter still holds if one
       only considers open neighborhoods.  (Contributed by Jeff Hankins,
       4-Sep-2009.) $)
    flimopn $p |- ( ( ( J e. Top /\ F e. Fil /\ X = Y ) /\ A e. X ) -> ( A e. (
    ( fLim ` J ) ` F ) <-> A. o e. J ( A e. o -> o e. F ) ) ) $=
      ( vn vs wcel wceq wa cfv wss cv wi wb adantr com23 ctop cfil w3a csn cnei
      wral flimelbas ex pm4.71rd elflim bitr3d pm5.32 sylibr imp opnneip 3expib
      cflim ssel syl9 exp4a 3ad2antl1 ralrimdv snssi isnei sylan2 eleq2 imbi12d
      wrex eleq1 rcla4v adantl snssg anbi1d ad2antlr pm2.27 sseq2 fillsb sylbid
      3expd com24 3adant1 ad2antrr com13 syl6 com12 imp3a sylbird syld impancom
      rexlimdv ssrdv impbid bitrd ) DUAKZCUBKZEFLZUCZAEKZMZACDUQNNKZAUDZDUENNZC
      OZABPZKZXDCKZQZBDUFZWQWRWTXCRZWQWRWTMZWRXCMZRWRXIQWQWTXJXKWQWTWRWQWTWRACD
      EFGHUGUHUIACDEFGHUJUKWRWTXCULUMUNWSXCXHWSXCXGBDWNWOWRXCXDDKZXGQQWPWNWRMZX
      CXLXEXFXMXLXEMZXDXBKZXCXFWNXNXOQWRWNXLXEXOADXDUOUPSXBCXDURUSUTVAVBWSXHXCW
      SXHMZIXBCXPIPZXBKZXQEOZXAJPZOZXTXQOZMZJDVHZMZXQCKZWSXRYERZXHWNWOWRYGWPWRW
      NXAEOYGAEVCXAJDXQEGVDVEVASXPXSYDYFXPYDXSYFXPYCXSYFQZJDWSXTDKZXHYCYHQZWSYI
      MZXHAXTKZXTCKZQZYJYIXHYNQWSXGYNBXTDXDXTLXEYLXFYMXDXTAVFXDXTCVIVGVJVKYKYCY
      NYHYKYCYLYBMZYNYHQZWRYOYCRWQYIWRYLYAYBAXTEVLVMVNYKYLYBYPYLYKYBYPQYLYNYBYK
      YHYLYNYMYBYKYHQQYLYMVOYKYBYMYHWQYBYMYHQQZWRYIWOWPYQWNWOWPMZXSYMYBYFYRXSXQ
      FOZYMYBYFQZQZWPXSYSRWOEFXQVPVKWOYSUUAQWPWOYMYSYTWOYMYSYBYFXTXQCFHVQVSTSVR
      VTWAWBWCWDVTWEWFWGTWHWIWJTWFVRWKUHWLWM $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    $d o A $.  $d o x B $.  $d o F $.  $d o J $.  $d o X $.  $d o Y $.
    fbaslim.1 $e |- X = U. J $.
    fbaslim.2 $e |- Y = U. B $.
    fbaslim.3 $e |- F = ( filGen ` B ) $.
    $( A condition for a filter to converge to a point involving one of its
       bases.  (Contributed by Jeff Hankins, 4-Sep-2009.) $)
    fbaslim $p |- ( ( ( J e. Top /\ B e. fBas /\ X = Y ) /\ A e. X ) -> ( A e.
    ( ( fLim ` J ) ` F ) <-> A. o e. J ( A e. o -> E. x e. B x C_ o ) ) ) $=
      ( wcel wceq w3a wa cfv wi wss wb adantr ctop cfbas cv wral wrex cfil cuni
      cflim simp1 fgfil syl5eqel 3ad2ant2 simp3 fgbas unieqi syl6eqr eqtrd 3jca
      cfg eqid flimopn sylan eltopss 3ad2ant1 sseq2 3ad2ant3 biimpa eleq2i elfg
      ex syl5bb ibar adantl bitr4d 3ad2antl2 syldan syld pm5.74d ralbidv2 bitrd
      imbi2d ) FUALZCUBLZGHMZNZBGLZOZBEFUHPPLZBDUCZLZWIELZQZDFUDZWJAUCWIRACUEZQ
      ZDFUDWEWBEUFLZGEUGZMZNWFWHWMSWEWBWPWRWBWCWDUIWCWBWPWDWCECUSPZUFKCUJUKULWE
      GHWQWBWCWDUMWCWBHWQMWDWCHWSUGWQCHJUNEWSKUOUPULUQURBDEFGWQIWQUTVAVBWGWLWOD
      FFWGWIFLZWLWOWGWTWIGRZWLWOSZWEWTXAQZWFWBWCXCWDWBWTXAWIFGIVCVJVDTWEXAXBQWF
      WEXAXBWEXAOWKWNWJWEXAWIHRZWKWNSZWEXAXDWDWBXAXDSWCGHWIVEVFVGWCWBXDXEWDWCXD
      OWKXDWNOZWNWCWKXFSXDWKWIWSLWCXFEWSWIKVHAWICHJVIVKTXDWNXFSWCXDWNVLVMVNVOVP
      WAVJTVQVRVSVT $.
      $( [4-Sep-2009] $)
  $}

  ${
    $d o A $.  $d o s F $.  $d o J $.  $d o s L $.  $d o s X $.  $d o s Y $.
    isflimf.1 $e |- X = U. J $.
    isflimf.2 $e |- Y = U. L $.
    $( The property of being a limit point of a function.  (Contributed by Jeff
       Hankins, 8-Nov-2009.) $)
    isflimf $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) -> ( A e. ( ( J
    fLimf L ) ` F ) <-> ( A e. X /\ A. o e. J ( A e. o -> E. s e. L ( F " s )
    C_ o ) ) ) ) $=
      ( ctop wcel cfil w3a co cfv wa cvv syl3anc wb wf cflf cfm cflim wral cima
      cv wss wrex flfval eleq2d simpl cuni simp1 cfbas uniexg syl5eqel 3ad2ant1
      wi wceq filfbas 3ad2ant2 simp3 fmfil fmbas eqcomd 3jca eqid flimelbas jca
      sylan anim2i flimopn ibar adantl pm5.21nd elfm adantr syl6sseqr biantrurd
      bitrd elssuni bitr4d imbi2d ralbidva anbi2d 3bitrd ) DKLZEMLZGFCUAZNZACDE
      UBOPZLACFEUCOPZDUDPPZLZAFLZABUGZLZWQWMLZUSZBDUEZQZWPWRCHUGUFWQUHHEUIZUSZB
      DUEZQWKWLWNACDEFGIJUJUKWKWOXBWKWPQZWKWOQWKWPWKWOULWKWHWMMLZFWMUMZUTZNZWOW
      PWKWHXGXIWHWIWJUNWKFRLZEUOLZWJXGWHWIXKWJWHFDUMZRIDKUPUQURZWIWHXLWJEVAVBZW
      HWIWJVCZRECFGJVDSWKXKXLWJXIXNXOXPXKXLWJNXHFRECFGJVEVFSVGZAWMDFXHIXHVHZVIV
      KVJXBWPWKWPXAULVLXFWOXAXBWKXJWPWOXATXQABWMDFXHIXRVMVKWPXAXBTWKWPXAVNVOWAV
      PWKXAXEWPWKWTXDBDWKWQDLZQZWSXCWRXTWSWQFUHZXCQZXCWKWSYBTZXSWKXKXLWJYCXNXOX
      PHWQERCFGJVQSVRXSXCYBTWKXSYAXCXSWQXMFWQDWBIVSVTVOWCWDWEWFWG $.
      $( [1-Dec-2010] $) $( [8-Nov-2009] $)
  $}

  ${
    $d o s A $.  $d o s F $.  $d o s J $.  $d o s L $.  $d o s X $.
    $d o s Y $.
    flimfelbas.1 $e |- X = U. J $.
    flimfelbas.2 $e |- Y = U. L $.
    $( A limit point of a function is in the topological space.  (Contributed
       by Jeff Hankins, 10-Nov-2009.) $)
    flimfelbas $p |- ( ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) /\ A e. ( ( J
    fLimf L ) ` F ) ) -> A e. X ) $=
      ( vo vs ctop wcel cfil wf w3a cflf co cfv cv cima wss wi isflimf simprbda
      wrex wral ) CKLDMLFEBNOABCDPQRLAELAISZLBJSTUGUAJDUEUBICUFAIBCDEFJGHUCUD
      $.
      $( [1-Dec-2010] $) $( [10-Nov-2009] $)
  $}

  ${
    $d F f x $.  $d J f x $.  $d X f x $.
    hausflim2.1 $e |- X = U. J $.
    $( A condition for a topology to be Hausdorff in terms of filters.  A
       topology is Hausdorff iff every filter has at most one limit point.
       (Contributed by FL, 6-Dec-2010.) $)
    hausflim2 $p |- ( ( J e. Haus /\ F e. Fil /\ X = U. F ) -> E*
    x x e. ( ( fLim ` J ) ` F ) ) $=
      ( vf cha wcel cfil cuni wceq cv cflim cfv wmo ctop haustop wral hausflim
      wi unieq eqeq2d ax-17 fveq2 eleq2d mobid imbi12d rcla4cv syl6bi mpcom
      3imp ) CGHZBIHZDBJZKZALZBCMNZNZHZAOZCPHZULUMUOUTTZTZCQVAULDFLZJZKZUPVDUQN
      ZHZAOZTZFIRVCAFCDESVJVBFBIVDBKZVFUOVIUTVKVEUNDVDBUAUBVKVHUSAVKAUCVKVGURUP
      VDBUQUDUEUFUGUHUIUJUK $.
      $( [14-Nov-2010] $)
  $}

  ${
    holimf.1 $e |- Y = U. L $.
    holimf.2 $e |- X = U. J $.
    ${
      $d F x $.  $d J x $.  $d L x $.  $d X x $.  $d Y x $.
      $( If a function has its values in a Hausdorff space then it has at most
         one limit value.  (Contributed by FL, 6-Dec-2010.) $)
      holimf $p |- ( ( L e. Fil /\ J e. Haus /\ F : Y --> X )
       -> E* x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cha wf co cfv wmo cuni wceq cvv 3ad2ant2 syl3anc w3a cv cfm
        cflf cflim simp2 cfbas uniexg syl5eqel filfbas simp3 fmfil fmbas eqcomd
        3ad2ant1 hausflim2 ax-17 ctop haustop simp1 flfval eleq2d mobid mpbird
        ) DIJZCKJZFEBLZUAZAUBZBCDUDMNZJZAOVIBEDUCMNZCUENNZJZAOZVHVFVLIJZEVLPZQV
        OVEVFVGUFVHERJZDUGJZVGVPVFVEVRVGVFECPRHCKUHUISZVEVFVSVGDUJUOZVEVFVGUKZR
        DBEFGULTVHVQEVHVRVSVGVQEQVTWAWBRDBEFGUMTUNAVLCEHUPTVHVKVNAVHAUQVHVJVMVI
        VHCURJZVEVGVJVMQVFVEWCVGCUSSVEVFVGUTWBBCDEFHGVATVBVCVD $.
        $( [14-Nov-2010] $)
    $}

    ${
      $d F x $.  $d J x $.  $d L x $.  $d X x $.  $d Y x $.
      $( If a convergent function has its values in a Hausdorff space then it
         has only one limit value.  (Contributed by FL, 6-Dec-2010.) $)
      holimf2 $p |- ( ( ( L e. Fil /\ J e. Haus /\ F : Y --> X ) /\
                   ( ( J fLimf L ) ` F ) =/= (/) )
       -> E! x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cha wf w3a cflf co cfv c0 wne wa cv holimf adantr wb n0moeu
        wmo weu adantl mpbid ) DIJCKJFEBLMZBCDNOPZQRZSATUJJZAUEZULAUFZUIUMUKABC
        DEFGHUAUBUKUMUNUCUIAUJUDUGUH $.
        $( [14-Nov-2010] $)
    $}
  $}

