$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Path homotopy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c PHtpy $.
  $c ~=ph $.

  $( Extend class notation with the class of path homotopies between two
     continuous functions. $)
  cphtpy $a class PHtpy $.

  $( Extend class notation with the path homotopy relation. $)
  cphtpc $a class ~=ph $.

  ${
    $d f g h k s x $.
    $( Define the function which takes a topological space ` X ` and two
       continuous functions ` F : II --> X ` and ` G : II --> X ` and returns
       the class of path homotopies from ` F ` to ` G ` .  Definition of
       [Hatcher] p. 25. $)
    df-phtpy $a |- PHtpy = ( x e. Top |->
        ( f e. ( II Cn x ) , g e. { k e. ( II Cn x ) |
          ( ( f ` 0 ) = ( k ` 0 ) /\ ( f ` 1 ) = ( k ` 1 ) ) } |->
            { h e. ( ( II tX II ) Cn x ) | A. s e. ( 0 [,] 1 )
              ( ( ( s h 0 ) = ( f ` s ) /\ ( s h 1 ) = ( g ` s ) ) /\
                ( ( 0 h s ) = ( f ` 0 ) /\ ( 1 h s ) = ( f ` 1 ) ) ) } ) ) $.
  $}

  ${
    $d f g h k s t F $.  $d f g h j k s t x y J $.  $d t K $.  $d t x y z ph $.
    $d f g h k s t G $.  $d h s t x y H $.
    phtpyfval.1 $e |- ( ph -> J e. Top ) $.
    $( Value of the path homotopy function on a topology.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    phtpyfval $p |- ( ph -> ( PHtpy ` J ) =
        ( f e. ( II Cn J ) , g e. { k e. ( II Cn J ) |
          ( ( f ` 0 ) = ( k ` 0 ) /\ ( f ` 1 ) = ( k ` 1 ) ) } |->
            { h e. ( ( II tX II ) Cn J ) | A. s e. ( 0 [,] 1 )
              ( ( ( s h 0 ) = ( f ` s ) /\ ( s h 1 ) = ( g ` s ) ) /\
                ( ( 0 h s ) = ( f ` 0 ) /\ ( 1 h s ) = ( f ` 1 ) ) ) } ) ) $=
      ( vx wcel cfv cii ccn co cc0 cv wceq c1 wa crab ctop cphtpy cicc wral ctx
      vj cmpt2 oveq2 rabeq mpt2eq123dv df-phtpy ovex mpt2ex copab2 ssrab2 sseli
      syl anim2i anim1i ssoprab2i df-mpt2 3sstr4i ssexi fvmpt ) AFUAJFUBKBCLFMN
      ZOBPZKZOEPZKQRVFKZRVHKQSZEVETZGPZODPZNVLVFKQVLRVMNVLCPZKQSOVLVMNVGQRVLVMN
      VIQSSGORUCNUDZDLLUENZFMNZTZUGZQHUFFBCLUFPZMNZVJEWATZVODVPVTMNZTZUGVSUAUBV
      TFQZBCWAWBWDVEVKVRVTFLMUHZWEWAVEQWBVKQWFVJEWAVEUIUQWEWCVQQWDVRQVTFVPMUHVO
      DWCVQUIUQUJUFBCDEGUKVSBCVEVEVRUGZBCVEVEVRLFMULZWHUMVFVEJZVNVKJZSZIPVRQZSZ
      BCIUNWIVNVEJZSZWLSZBCIUNVSWGWMWPBCIWKWOWLWJWNWIVKVEVNVJEVEUOUPURUSUTBCIVE
      VKVRVABCIVEVEVRVAVBVCVDUQ $.
      $( [6-Jun-2014] $) $( [2-Sep-2009] $)

    phtpyval.2 $e |- ( ph -> F e. ( II Cn J ) ) $.
    phtpyval.3 $e |- ( ph -> G e. ( II Cn J ) ) $.
    phtpyval.4 $e |- ( ph -> ( F ` 0 ) = ( G ` 0 ) ) $.
    phtpyval.5 $e |- ( ph -> ( F ` 1 ) = ( G ` 1 ) ) $.
    $( The class of path homotopies between two continuous functions.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    phtpyval $p |- ( ph -> ( F ( PHtpy ` J ) G ) =
                     { h e. ( ( II tX II ) Cn J ) | A. s e. ( 0 [,] 1 )
                       ( ( ( s h 0 ) = ( F ` s ) /\ ( s h 1 ) = ( G ` s ) )
                 /\ ( ( 0 h s ) = ( F ` 0 ) /\ ( 1 h s ) = ( F ` 1 ) ) ) } ) $=
      ( vk cfv co cc0 cv wceq c1 wa eqeq2d vf cphtpy cii ccn crab cicc wral ctx
      vg cmpt2 phtpyfval oveqd wcel cvv fveq1 anbi12d elrab sylanbrc ovex rabex
      jca simpl fveq1d simpr ralbidv rabbidv eqeq1d eqid ovmpt2x syl3anc eqtrd
      a1i ) ACDEUBMZNCDUAUIUCEUDNZOUAPZMZOLPZMZQZRVOMZRVQMZQZSZLVNUEZFPZOBPZNZW
      EVOMZQZWERWFNZWEUIPZMZQZSZOWEWFNZVPQZRWEWFNZVTQZSZSZFORUFNZUGZBUCUCUHNZEU
      DNZUEZUJZNZWGWECMZQZWJWEDMZQZSZWOOCMZQZWQRCMZQZSZSZFXAUGZBXDUEZAVMXFCDAUA
      UIBLEFGUKULACVNUMDXMVRQZXOWAQZSZLVNUEZUMZXTUNUMZXGXTQHADVNUMXMODMZQZXORDM
      ZQZSZYEIAYHYJJKVAYCYKLDVNVQDQZYAYHYBYJYLVRYGXMOVQDUOTYLWAYIXORVQDUOTUPUQU
      RYFAXSBXDXCEUDUSUTVLUAUICDVNWDXEXTXFUNYDVOCQZWKDQZSZXBXSBXDYOWTXRFXAYOWNX
      LWSXQYOWIXIWMXKYOWHXHWGYOWEVOCYMYNVBZVCTYOWLXJWJYOWEWKDYMYNVDVCTUPYOWPXNW
      RXPYOVPXMWOYOOVOCYPVCTYOVTXOWQYORVOCYPVCTUPUPVEVFYMWCYCLVNYMVSYAWBYBYMVPX
      MVROVOCUOVGYMVTXOWARVOCUOVGUPVFXFVHVIVJVK $.
      $( [6-Jun-2014] $) $( [2-Sep-2009] $)

    $( Membership in the class of path homotopies between two continuous
       functions.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    isphtpy $p |- ( ph -> ( H e. ( F ( PHtpy ` J ) G ) <->
                    ( H e. ( ( II tX II ) Cn J ) /\ A. s e. ( 0 [,] 1 )
                      ( ( ( s H 0 ) = ( F ` s ) /\ ( s H 1 ) = ( G ` s ) )
              /\ ( ( 0 H s ) = ( F ` 0 ) /\ ( 1 H s ) = ( F ` 1 ) ) ) ) ) ) $=
      ( vh cfv co cc0 wceq c1 wa oveq eqeq1d cphtpy wcel cicc wral cii ctx crab
      cv ccn phtpyval eleq2d anbi12d ralbidv elrab syl6bb ) ADBCEUAMNZUBDFUHZOL
      UHZNZUQBMZPZUQQURNZUQCMZPZRZOUQURNZOBMZPZQUQURNZQBMZPZRZRZFOQUCNZUDZLUEUE
      UFNEUINZUGZUBDVPUBUQODNZUTPZUQQDNZVCPZRZOUQDNZVGPZQUQDNZVJPZRZRZFVNUDZRAU
      PVQDALBCEFGHIJKUJUKVOWILDVPURDPZVMWHFVNWJVEWBVLWGWJVAVSVDWAWJUSVRUTUQOURD
      STWJVBVTVCUQQURDSTULWJVHWDVKWFWJVFWCVGOUQURDSTWJVIWEVJQUQURDSTULULUMUNUO
      $.
      $( [6-Jun-2014] $) $( [2-Sep-2009] $)

    phtpycom.6 $e |- K = ( x e. ( 0 [,] 1 ) , y e. ( 0 [,] 1 ) |->
                           ( x H ( 1 - y ) ) ) $.
    phtpycom.7 $e |- ( ph -> H e. ( F ( PHtpy ` J ) G ) ) $.
    $( Given a homotopy from ` F ` to ` G ` , produce a homotopy from ` G ` to
       ` F ` .  (Contributed by Jeff Madsen, 2-Sep-2009.)  (The proof was
       shortened by Mario Carneiro, 6-Jun-2014.) $)
    phtpycom $p |- ( ph -> K e. ( G ( PHtpy ` J ) F ) ) $=
      ( co cii cc0 wceq c1 vt vz vs cphtpy cfv wcel ctx ccn cv cicc wral eqcomd
      wa isphtpy cmin cmpt2 iiuni ctop iitop a1i cnmpt1st cnmpt2nd cmpt iirevcn
      oveq2 cnmpt21 mpbid simpld cnmpt22f syl5eqel simpr 0elunit ax-1cn subid1i
      oveq1 syl6eq oveq2d ovmpt2 sylancl simprd r19.21bi 1elunit subidi sylancr
      eqtrd ralimi syl iirev eqeq1d anbi12d rcla4cva syl2an adantr 3eqtrd jca31
      ovex jca ralrimiva mpbir2and ) AHEDGUDUEZPUFHQQUGPGUHPZUFUAUIZRHPZXBEUEZS
      ZXBTHPZXBDUEZSZUMRXBHPZREUEZSZTXBHPZTEUEZSZUMZUMZUARTUJPZUKAEDHGUAIKJARDU
      EZXJLULATDUEZXMMULUNAHBCXQXQBUIZTCUIZUOPZFPZUPXANABCXTYBFQQQQGXQXQXQXQUQU
      QQURUFAUSUTZYDYDYDABCQQXQXQUQUQYDYDVAABCUBYATUBUIZUOPZYBQQQQXQXQXQUQUQYDY
      DYDYDABCQQXQXQUQUQYDYDVBUQUBXQYFVCQQUHPUFAUBVDUTYEYATUOVEVFUQUQIAFXAUFZUC
      UIZRFPYHDUESYHTFPYHEUESUMZRYHFPZXRSZTYHFPZXSSZUMZUMZUCXQUKZAFDEWTPUFZYGYP
      UMOADEFGUCIJKLMUNVGZVHVIVJAXPUAXQAXBXQUFZUMZXEXHXOYTXCXBTFPZXDYTYSRXQUFZX
      CUUASAYSVKZVLBCXBRXQXQYCUUAHXBYBFPZXTXBYBFVOZYARSZYBTXBFUUFYBTRUOPTYARTUO
      VETVMVNVPVQNXBTFWPVRVSYTXBRFPZXGSZUUAXDSZYTUUHUUIUMZRXBFPXRSTXBFPXSSUMZAU
      UJUUKUMZUAXQAYGUULUAXQUKZAYQYGUUMUMOADEFGUAIJKLMUNVGVTWAVHZVTWEYTXFUUGXGY
      TYSTXQUFZXFUUGSUUCWBBCXBTXQXQYCUUGHUUDUUEYATSZYBRXBFUUPYBTTUOPRYATTUOVETV
      MWCVPVQNXBRFWPVRVSYTUUHUUIUUNVHWEYTXKXNYTXIRTXBUOPZFPZXRXJYTUUBYSXIUURSVL
      UUCBCRXBXQXQYCUURHRYBFPXTRYBFVOYAXBSZYBUUQRFYAXBTUOVEZVQNRUUQFWPVRWDYTUUR
      XRSZTUUQFPZXSSZAYNUCXQUKZUUQXQUFUVAUVCUMZYSAYPUVDAYGYPYRVTYOYNUCXQYIYNVKW
      FWGXBWHYNUVEUCUUQXQYHUUQSZYKUVAYMUVCUVFYJUURXRYHUUQRFVEWIUVFYLUVBXSYHUUQT
      FVEWIWJWKWLZVHAXRXJSYSLWMWNYTXLUVBXSXMYTUUOYSXLUVBSWBUUCBCTXBXQXQYCUVBHTY
      BFPXTTYBFVOUUSYBUUQTFUUTVQNTUUQFWPVRWDYTUVAUVCUVGVTAXSXMSYSMWMWNWQWOWRWS
      $.
      $( [6-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d s x y F $.  $d s G $.  $d s x y J $.  $d x y ph $.
    phtpyid.1 $e |- G = ( x e. ( 0 [,] 1 ) , y e. ( 0 [,] 1 ) |->
                          ( F ` x ) ) $.
    phtpyid.2 $e |- ( ph -> J e. Top ) $.
    phtpyid.3 $e |- ( ph -> F e. ( II Cn J ) ) $.
    $( A homotopy from a path to itself.  (Contributed by Jeff Madsen,
       2-Sep-2009.)  (The proof was shortened by Mario Carneiro,
       6-Jun-2014.) $)
    phtpyid $p |- ( ph -> G e. ( F ( PHtpy ` J ) F ) ) $=
      ( vs cfv co wcel cii cc0 wceq c1 eqidd iiuni ovmpt2 cphtpy ctx cv wa cicc
      ccn wral isphtpy cmpt2 iitop a1i cnmpt1st cnmpt21f syl5eqel 0elunit fveq2
      ctop fvex mpan2 1elunit mpan jca jca31 rgen mpbir2and ) AEDDFUAKLMENNUBLF
      UFLZMJUCZOELVGDKZPZVGQELVHPZUDOVGELODKZPZQVGELQDKZPZUDZUDZJOQUELZUGZADDEF
      JHIIAVKRAVMRUHAEBCVQVQBUCZDKZUIVFGABCVSDNNNFVQVQSSNUQMAUJUKZWAWAHABCNNVQV
      QSSWAWAULIUMUNVRAVPJVQVGVQMZVIVJVOWBOVQMZVIUOBCVGOVQVQVTVHEVHVSVGDUPZCUCZ
      OPVHRGVGDURZTUSWBQVQMZVJUTBCVGQVQVQVTVHEVHWDWEQPVHRGWFTUSWBVLVNWCWBVLUOBC
      OVGVQVQVTVKEVKVSODUPWEVGPZVKRGODURTVAWGWBVNUTBCQVGVQVQVTVMEVMVSQDUPWHVMRG
      QDURTVAVBVCVDUKVE $.
      $( [6-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d s t F $.  $d s t G $.  $d s t x y J $.  $d s t x y K $.  $d s t x y L $.
    $d s t H $.  $d s M $.  $d s x y z ph $.
    phtpyco.1 $e |- M = ( x e. ( 0 [,] 1 ) , y e. ( 0 [,] 1 ) |->
                          if ( y <_ ( 1 / 2 ) ,
                    ( x K ( 2 x. y ) ) , ( x L ( ( 2 x. y ) - 1 ) ) ) ) $.
    phtpyco.2 $e |- ( ph -> J e. Top ) $.
    phtpyco.3 $e |- ( ph -> F e. ( II Cn J ) ) $.
    phtpyco.4 $e |- ( ph -> G e. ( II Cn J ) ) $.
    phtpyco.5 $e |- ( ph -> H e. ( II Cn J ) ) $.
    phtpyco.6 $e |- ( ph -> ( F ` 0 ) = ( G ` 0 ) ) $.
    phtpyco.7 $e |- ( ph -> ( F ` 1 ) = ( G ` 1 ) ) $.
    phtpyco.8 $e |- ( ph -> ( G ` 0 ) = ( H ` 0 ) ) $.
    phtpyco.9 $e |- ( ph -> ( G ` 1 ) = ( H ` 1 ) ) $.
    phtpyco.10 $e |- ( ph -> K e. ( F ( PHtpy ` J ) G ) ) $.
    phtpyco.11 $e |- ( ph -> L e. ( G ( PHtpy ` J ) H ) ) $.
    $( Concatenate two path homotopies.  (Contributed by Jeff Madsen,
       2-Sep-2009.)  (The proof was shortened by Mario Carneiro,
       7-Jun-2014.) $)
    phtpyco $p |- ( ph -> M e. ( F ( PHtpy ` J ) H ) ) $=
      ( vs vz vt cphtpy cfv co wcel cii ctx ccn cv wceq c1 wa cicc wral isphtpy
      cc0 eqtrd c2 cdiv cle wbr cmul cmin cif cmpt2 iiuni ctop a1i cioo crn ctg
      iitop cop csubsp eqid dfii2 0re 1re elicc2i rehalfcl ax-mp ltleii halflt1
      cr mpbid simprd r19.21bi simpld ralrimiva oveq2d 2cn syl6eq oveq1d ax-1cn
      oveq1 3eqtr4d cuni wss retop iccssre mp2an uniretop sseqtri stoig2 stoig3
      cvv ovex cnmpt2nd cnmpt1st cmpt oveq2 cnmpt21 cnmpt22f 0elunit iftrue syl
      simpr simpl oveq12d ovmpt2a sylancl 1elunit breq1d iffalse ifbieq12d ifex
      wn sylancr adantl adantll ralimi ad2antrr eqeq1d anbi12d rcla4v pm2.61dan
      sylc halfgt0 mpbir3an eqtr4d eqeq12d rcla4cva sylan adantrl simprl recidi
      subidi iihalf1cn iihalf2cn cnmpt2pc cnmptcom syl5eqel syl6eqbr mul01i clt
      2ne0 ltnlei mtbiri mulid1i 1p1e2 subaddrii elii1 iihalf1 sylbir elii2 jca
      mpbi iihalf2 jca31 mpbir2and ) AJDFGUEUFZUGUHJUIUIUJUGGUKUGZUHUBULZUSJUGZ
      UVPDUFZUMZUVPUNJUGZUVPFUFZUMZUOUSUVPJUGZUSDUFZUMZUNUVPJUGZUNDUFZUMZUOZUOZ
      UBUSUNUPUGZUQADFJGUBLMOAUWDUSEUFZUSFUFPRUTAUWGUNEUFZUNFUFQSUTURAJBCUWKUWK
      CULZUNVAVBUGZVCVDZBULZVAUWNVEUGZHUGZUWQUWRUNVFUGZIUGZVGZVHUVOKACBUXBUIUIG
      UWKUWKVIVIUIVJUHAVOVKZUXCLACBUSUWOUNUWSVLVMVNUFZUXAUIGUSUWOUPUGZUXDVPVQUF
      ZUWOUNUPUGZUXDVPVQUFZUIUWKUXDVRUXFVRZUXHVRZVSVIUSWGUHZAVTVKUNWGUHZAWAVKUW
      OUWKUHZAUXMUWOWGUHZUSUWOVCVDUWOUNVCVDUSUNUWOVTWAWBUXLUXNWAUNWCWDZUSUWOVTU
      XOUUAWEZUWOUNUXOWAWFWEUUBVKUXCLAUWNUWOUMZUWQUWKUHZUOUOZUWQUNHUGZUWQUSIUGZ
      UWSUXAAUXRUXTUYAUMZUXQAUVPUNHUGZUVPUSIUGZUMZUBUWKUQUXRUYBAUYEUBUWKAUVPUWK
      UHZUOZUYCUVPEUFZUYDUYGUVPUSHUGZUVRUMZUYCUYHUMZUYGUYJUYKUOZUSUVPHUGUWDUMUN
      UVPHUGUWGUMUOZAUYLUYMUOZUBUWKAHUVOUHZUYNUBUWKUQZAHDEUVNUGUHZUYOUYPUOTADEH
      GUBLMNPQURWHZWIWJWKZWIUYGUYDUYHUMZUVPUNIUGZUWAUMZUYGUYTVUBUOZUSUVPIUGUWLU
      MUNUVPIUGUWMUMUOZAVUCVUDUOZUBUWKAIUVOUHZVUEUBUWKUQZAIEFUVNUGUHZVUFVUGUOUA
      AEFIGUBLNORSURWHZWIWJWKZWKUUCWLUYEUYBUBUWQUWKUVPUWQUMUYCUXTUYDUYAUVPUWQUN
      HWRUVPUWQUSIWRUUDUUEUUFUUGUXSUWRUNUWQHUXSUWRVAUWOVEUGUNUXSUWNUWOVAVEAUXQU
      XRUUHWMVAWNUUSUUIWOZWMUXSUWTUSUWQIUXSUWTUNUNVFUGUSUXSUWRUNUNVFVUKWPUNWQUU
      JWOWMWSACBUWQUWRHUXFUIUIUIGUWKUXEUWKUWKUXDVJUHZUXEUXDWTZXAUXEUXFWTUMXBUXE
      WGVUMUXKUXNUXEWGXAVTUXOUSUWOXCXDXEXFUXEUXDXGXDZVIUXFVJUHZAVULUXEXIUHVUOXB
      USUWOUPXJUXEUXDXIXHXDVKZUXCUXCUXCACBUXFUIUXEUWKVUNVIVUPUXCXKACBUCUWNVAUCU
      LZVEUGZUWRUXFUIUXFUIUXEUWKUXEVUNVIVUPUXCVUPUXCACBUXFUIUXEUWKVUNVIVUPUXCXL
      VUNUCUXEVURXMUXFUIUKUGUHAUCUXFUXIUUKVKVUQUWNVAVEXNZXOVIVILAUYOUYPUYRWKXPA
      CBUWQUWTIUXHUIUIUIGUWKUXGUWKUWKVULUXGVUMXAUXGUXHWTUMXBUXGWGVUMUXNUXLUXGWG
      XAUXOWAUWOUNXCXDXEXFUXGUXDXGXDZVIUXHVJUHZAVULUXGXIUHVVAXBUWOUNUPXJUXGUXDX
      IXHXDVKZUXCUXCUXCACBUXHUIUXGUWKVUTVIVVBUXCXKACBUCUWNVURUNVFUGZUWTUXHUIUXH
      UIUXGUWKUXGVUTVIVVBUXCVVBUXCACBUXHUIUXGUWKVUTVIVVBUXCXLVUTUCUXGVVCXMUXHUI
      UKUGUHAUCUXHUXJUULVKVUQUWNUMVURUWRUNVFVUSWPXOVIVILAVUFVUGVUIWKXPUUMUUNUUO
      AUWJUBUWKUYGUVSUWBUWIUYGUVQUYIUVRUYGUYFUSUWKUHZUVQUYIUMAUYFXTZXQBCUVPUSUW
      KUWKUXBUYIJUWQUVPUMZUWNUSUMZUOZUXBUWSUYIVVHUWPUXBUWSUMVVHUWNUSUWOVCVVFVVG
      XTZUXPUUPUWPUWSUXAXRXSVVHUWQUVPUWRUSHVVFVVGYAVVHUWRVAUSVEUGUSVVHUWNUSVAVE
      VVIWMVAWNUUQWOYBUTKUVPUSHXJYCYDUYGUYJUYKUYSWKUTUYGUVTVUAUWAUYGUYFUNUWKUHZ
      UVTVUAUMVVEYEBCUVPUNUWKUWKUXBVUAJVVFUWNUNUMZUOZUXBUXAVUAVVLUWPYJUXBUXAUMV
      VLUWPUNUWOVCVDZUWOUNUURVDVVMYJWFUWOUNUXOWAUUTUVJVVLUWNUNUWOVCVVFVVKXTZYFU
      VAUWPUWSUXAYGXSVVLUWQUVPUWTUNIVVFVVKYAVVLUWTVAUNVFUGUNVVLUWRVAUNVFVVLUWRV
      AUNVEUGVAVVLUWNUNVAVEVVNWMVAWNUVBWOWPVAUNUNWNWQWQUVCUVDWOYBUTKUVPUNIXJYCY
      DUYGUYTVUBVUJWIUTUYGUWEUWHUYGUWCUVPUWOVCVDZUSVAUVPVEUGZHUGZUSVVPUNVFUGZIU
      GZVGZUWDUYGVVDUYFUWCVVTUMXQVVEBCUSUVPUWKUWKUXBVVTJUWQUSUMZUWNUVPUMZUOZUWP
      VVOUWSUXAVVQVVSVWCUWNUVPUWOVCVWAVWBXTZYFVWCUWQUSUWRVVPHVWAVWBYAZVWCUWNUVP
      VAVEVWDWMZYBVWCUWQUSUWTVVRIVWEVWCUWRVVPUNVFVWFWPYBYHKVVOVVQVVSUSVVPHXJUSV
      VRIXJYIYCYKUYGVVOVVTUWDUMUYGVVOUOZVVTVVQUWDVVOVVTVVQUMUYGVVOVVQVVSXRYLVWG
      VVQUWDUMZUNVVPHUGZUWGUMZVWGVVPUWKUHZUSUDULZHUGZUWDUMZUNVWLHUGZUWGUMZUOZUD
      UWKUQZVWHVWJUOZUYFVVOVWKAUYFVVOUOUVPUXEUHVWKUVPUVEUVPUVFUVGYMAVWRUYFVVOAV
      WLUSHUGVWLDUFUMVWLUNHUGVWLEUFZUMUOZVWQUOZUDUWKUQZVWRAUYOVXCAUYQUYOVXCUOTA
      DEHGUDLMNPQURWHWIVXBVWQUDUWKVXAVWQXTYNXSYOVWQVWSUDVVPUWKVWLVVPUMZVWNVWHVW
      PVWJVXDVWMVVQUWDVWLVVPUSHXNYPVXDVWOVWIUWGVWLVVPUNHXNYPYQYRYTZWKUTUYGVVOYJ
      ZUOZVVSUWLVVTUWDVXGVVSUWLUMZUNVVRIUGZUWMUMZVXGVVRUWKUHZUSVWLIUGZUWLUMZUNV
      WLIUGZUWMUMZUOZUDUWKUQZVXHVXJUOZUYFVXFVXKAUYFVXFUOUVPUXGUHVXKUVPUVHUVPUVK
      XSYMAVXQUYFVXFAVWLUSIUGVWTUMVWLUNIUGVWLFUFUMUOZVXPUOZUDUWKUQZVXQAVUFVYAAV
      UHVUFVYAUOUAAEFIGUDLNORSURWHWIVXTVXPUDUWKVXSVXPXTYNXSYOVXPVXRUDVVRUWKVWLV
      VRUMZVXMVXHVXOVXJVYBVXLVVSUWLVWLVVRUSIXNYPVYBVXNVXIUWMVWLVVRUNIXNYPYQYRYT
      ZWKVXFVVTVVSUMUYGVVOVVQVVSYGYLAUWDUWLUMUYFVXFPYOWSYSUTUYGUWFVVOVWIVXIVGZU
      WGUYGVVJUYFUWFVYDUMYEVVEBCUNUVPUWKUWKUXBVYDJUWQUNUMZVWBUOZUWPVVOUWSUXAVWI
      VXIVYFUWNUVPUWOVCVYEVWBXTZYFVYFUWQUNUWRVVPHVYEVWBYAZVYFUWNUVPVAVEVYGWMZYB
      VYFUWQUNUWTVVRIVYHVYFUWRVVPUNVFVYIWPYBYHKVVOVWIVXIUNVVPHXJUNVVRIXJYIYCYKU
      YGVVOVYDUWGUMVWGVYDVWIUWGVVOVYDVWIUMUYGVVOVWIVXIXRYLVWGVWHVWJVXEWIUTVXGVX
      IUWMVYDUWGVXGVXHVXJVYCWIVXFVYDVXIUMUYGVVOVWIVXIYGYLAUWGUWMUMUYFVXFQYOWSYS
      UTUVIUVLWLUVM $.
      $( [7-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x f g $.
    $( Define the function which takes a topology and returns the path homotopy
       relation on that topology.  Definition of [Hatcher] p. 25. $)
    df-phtpc $a |- ~=ph = ( x e. Top |-> { <. f , g >. |
        ( ( ( f e. ( II Cn x ) /\ g e. ( II Cn x ) ) /\ ( ( f ` 0 ) = ( g ` 0 )
                        /\ ( f ` 1 ) = ( g ` 1 ) ) ) /\
                  ( f ( PHtpy ` x ) g ) =/= (/) ) } ) $.
  $}

  ${
    $d f g j J $.
    $( The value of the path homotopy relation.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    phtpcval $p |- ( J e. Top -> ( ~=ph ` J ) = { <. f , g >. |
        ( ( ( f e. ( II Cn J ) /\ g e. ( II Cn J ) ) /\ ( ( f ` 0 ) = ( g ` 0 )
        /\ ( f ` 1 ) = ( g ` 1 ) ) ) /\ ( f ( PHtpy ` J ) g ) =/= (/) ) } ) $=
      ( vj cv cii ccn co wcel wa cc0 cfv wceq c1 cphtpy c0 copab eleq2d anbi12d
      wne ctop cphtpc oveq2 anbi1d fveq2 neeq1d opabbidv df-phtpc cxp ovex xpex
      oveqd simpll ssopab2i df-xp sseqtr4i ssexi fvmpt ) DCAEZFDEZGHZIZBEZVAIZJ
      ZKUSLKVCLMNUSLNVCLMJZJZUSVCUTOLZHZPTZJZABQUSFCGHZIZVCVLIZJZVFJZUSVCCOLZHZ
      PTZJZABQZUAUBUTCMZVKVTABWBVGVPVJVSWBVEVOVFWBVBVMVDVNWBVAVLUSUTCFGUCZRWBVA
      VLVCWCRSUDWBVIVRPWBVHVQUSVCUTCOUEULUFSUGDABUHWAVLVLUIZVLVLFCGUJZWEUKWAVOA
      BQWDVTVOABVOVFVSUMUNABVLVLUOUPUQUR $.
      $( [7-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J f g j $.
    $( The path homotopy relation is a relation.  (Contributed by Mario
       Carneiro, 7-Jun-2014.) $)
    phtpcrel $p |- Rel ( ~=ph ` J ) $=
      ( vf vj vg cv cii ccn co wcel wa cc0 cfv wceq c1 cphtpy wne ctop df-phtpc
      c0 cphtpc relmptopab ) BEZFCEZGHZIDEZUDIJKUBLKUELMNUBLNUELMJJUBUEUCOLHSPJ
      CBDQATCBDRUA $.
      $( [7-Aug-2014] $) $( [7-Jun-2014] $)
  $}

  ${
    $d J f g $.  $d F f g $.  $d G f g $.
    $( The relation "is path homotopic to".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isphtpc $p |- ( ( J e. Top /\ G e. V ) -> ( F ( ~=ph ` J ) G <->
        ( ( ( F e. ( II Cn J ) /\ G e. ( II Cn J ) ) /\ ( ( F ` 0 ) = ( G ` 0 )
        /\ ( F ` 1 ) = ( G ` 1 ) ) ) /\ ( F ( PHtpy ` J ) G ) =/= (/) ) ) ) $=
      ( vf vg wcel wa cvv cfv wbr co cc0 wceq c1 c0 wne wi fveq1 anbi12d cphtpc
      ctop cii ccn cphtpy phtpcrel brrelexi elex ad3antrrr wb cv copab phtpcval
      a1i breqd eleq1 anbi1d eqeq1d oveq1 neeq1d anbi2d eqeq2d oveq2 eqid brabg
      ancoms sylan9bb expr pm5.21ndd ) CUBGZBDGZHZAIGZABCUAJZKZAUCCUDLZGZBVPGZH
      ZMAJZMBJZNZOAJZOBJZNZHZHZABCUEJZLZPQZHZVOVMRVLABVNCUFUGUNWKVMRVLVQVMVRWFW
      JAVPUHUIUNVJVKVMVOWKUJVJVOABEUKZVPGZFUKZVPGZHZMWLJZMWNJZNZOWLJZOWNJZNZHZH
      ZWLWNWHLZPQZHZEFULZKZVKVMHWKVJVNXHABEFCUMUOVMVKXIWKUJXGVQWOHZVTWRNZWCXANZ
      HZHZAWNWHLZPQZHWKEFABIDXHWLANZXDXNXFXPXQWPXJXCXMXQWMVQWOWLAVPUPUQXQWSXKXB
      XLXQWQVTWRMWLASURXQWTWCXAOWLASURTTXQXEXOPWLAWNWHUSUTTWNBNZXNWGXPWJXRXJVSX
      MWFXRWOVRVQWNBVPUPVAXRXKWBXLWEXRWRWAVTMWNBSVBXRXAWDWCOWNBSVBTTXRXOWIPWNBA
      WHVCUTTXHVDVEVFVGVHVI $.
      $( [7-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J w x y z $.
    $( The domain of the path homotopy relation.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    phtpcdm $p |- ( J e. Top -> dom ( ~=ph ` J ) = ( II Cn J ) ) $=
      ( vx vy vz vw wcel cfv co cv wbr vex wa cc0 wceq c1 wne cvv isphtpc eqid
      c0 ctop cphtpc cdm cii ccn wex eldm cphtpy wb mpan2 simplll syl6bi syl5bi
      exlimdv pm3.2 pm2.43i pm3.2i jctir adantl cicc cmpt2 simpl simpr ne0i syl
      phtpyid mpbir2and breldm ex impbid eqrdv ) AUAFZBAUBGZUCZUDAUEHZVLBIZVNFZ
      VPVOFZVQVPCIZVMJZCUFVLVRCVPVMBKZUGVLVTVRCVLVTVRVSVOFZLMVPGZMVSGNOVPGZOVSG
      NLZLVPVSAUHGZHTPZLZVRVLVSQFVTWHUICKVPVSAQRUJVRWBWEWGUKULUNUMVLVRVQVLVRLZV
      PVPVMJZVQWIWJVRVRLZWCWCNZWDWDNZLZLZVPVPWFHZTPZVPVPAVORVRWOVLVRWKWNVRWKVRV
      RUOUPWLWMWCSWDSUQURUSWIDEMOUTHZWRDIVPGVAZWPFWQWIDEVPWSAWSSVLVRVBVLVRVCVFW
      PWSVDVEVGVPVPVMWAVHVEVIVJVK $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d f g u v x y z J $.
    $( Path homotopy is an equivalence relation.  Proposition 1.2 of [Hatcher]
       p. 26.  (Contributed by Jeff Madsen, 2-Sep-2009.)  (The proof was
       shortened by Mario Carneiro, 7-Jun-2014.) $)
    phtpcer $p |- ( J e. Top -> Er ( ~=ph ` J ) ) $=
      ( vf vu vv vg wcel cfv cv wbr wa co cc0 wceq wne cvv adantr simpld simprd
      c1 c0 vx vy vz ctop cphtpc cii ccn cphtpy vex isphtpc mpan2 biimpa ancomd
      wb eqcomd jca32 wex sylib cicc cmin cmpt2 simpll eqid simpr phtpycom ne0i
      n0 syl ex exlimdv mpd mpbir2and adantrr adantrl eqtrd eeanv sylanbrc cdiv
      jca c2 cle cmul cif simprl simprr phtpyco exlimdvv iserd ) AUDFZUAUBUCAUE
      GZWIUAHZUBHZWJIZJZWLWKWJIZWLUFAUGKZFZWKWPFZJZLWLGZLWKGZMZSWLGZSWKGZMZJJZW
      LWKAUHGZKZTNZWIWOXFXIJUNZWMWIWKOFXJUAUIWLWKAOUJUKPWNWSXBXEWNWRWQWNWRWQJZX
      AWTMZXDXCMZJZWNXKXNJZWKWLXGKZTNZWIWMXOXQJZWIWLOFWMXRUNUBUIWKWLAOUJUKULZQZ
      QZUMWNXAWTWNXLXMWNXKXNXTRZQZUOWNXDXCWNXLXMYBRZUOUPWNBHZXPFZBUQZXIWNXQYGWN
      XOXQXSRZBXPVGZURWNYFXIBWNYFXIWNYFJZCDLSUSKZYKCHZSDHZUTKYEKVAZXHFXIYJCDWKW
      LYEAYNWIWMYFVBWNWRYFWNWRWQYAQZPWNWQYFWNWRWQYARPWNXLYFYCPWNXMYFYDPYNVCWNYF
      VDVEXHYNVFVHVIVJVKVLWIWMWLUCHZWJIZJZJZWKYPWJIZWRYPWPFZJZXALYPGZMZXDSYPGZM
      ZJJZWKYPXGKZTNZWIYTUUGUUIJUNZYRWIYPOFZUUJUCUIZWKYPAOUJUKPYSUUBUUDUUFYSWRU
      UAWIWMWRYQYOVMZYSWQUUAYSWQUUAJZWTUUCMZXCUUEMZJZYSUUNUUQJZWLYPXGKZTNZWIYQU
      URUUTJZWMWIYQUVAWIUUKYQUVAUNUULWLYPAOUJUKULVNZQZQZRZVSYSXAWTUUCWIWMXLYQYC
      VMZYSUUOUUPYSUUNUUQUVCRZQZVOYSXDXCUUEWIWMXMYQYDVMZYSUUOUUPUVGRZVOUPYSYFEH
      ZUUSFZJZEUQBUQZUUIYSYGUVLEUQZUVNYSXQYGWIWMXQYQYHVMYIURYSUUTUVOYSUURUUTUVB
      REUUSVGURYFUVLBEVPVQYSUVMUUIBEYSUVMUUIYSUVMJZCDYKYKYMSVTVRKWAIYLVTYMWBKZY
      EKYLUVQSUTKUVKKWCVAZUUHFUUIUVPCDWKWLYPAYEUVKUVRUVRVCWIYRUVMVBYSWRUVMUUMPY
      SWQUVMYSWQUUAUVDQPYSUUAUVMUVEPYSXLUVMUVFPYSXMUVMUVIPYSUUOUVMUVHPYSUUPUVMU
      VJPYSYFUVLWDYSYFUVLWEWFUUHUVRVFVHVIWGVKVLWH $.
      $( [7-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d s x y F $.  $d s x y G $.  $d s H $.  $d s x y J $.  $d s x y z ph $.
    reparpht.1 $e |- ( ph -> J e. Top ) $.
    reparpht.2 $e |- ( ph -> F e. ( II Cn J ) ) $.
    reparpht.3 $e |- ( ph -> G e. ( II Cn II ) ) $.
    reparpht.4 $e |- ( ph -> ( G ` 0 ) = 0 ) $.
    reparpht.5 $e |- ( ph -> ( G ` 1 ) = 1 ) $.
    ${
      reparphti.6 $e |- H = ( x e. ( 0 [,] 1 ) , y e. ( 0 [,] 1 ) |->
                    ( F ` ( ( ( 1 - y ) x. ( G ` x ) ) + ( y x. x ) ) ) ) $.
      $( Lemma for ~ reparpht . $)
      reparphti $p |- ( ph -> H e. ( ( F o. G ) ( PHtpy ` J ) F ) ) $=
        ( co wcel cii cc0 wceq c1 cmul vs vz ccom cphtpy cfv ctx ccn cv wa cicc
        wral ctop iitop a1i cnco syl32anc wfun cuni iiuni eqid cnf syl3anc ffun
        wf syl 0elunit fvco3 fveq2d eqtrd 1elunit isphtpy cmin caddc cmpt2 cabs
        copn cop csubsp cc cme cnmet opntop wss txtopi cr 0re 1re iccssre mp2an
        sstri cnmpt2nd cmpt iirevcn oveq2 cnmpt21 dfii3 oveq2i syl6eleq cnmetba
        ax-resscn uniopn2 ax-mp syl22anc cnmpt1st cnmpt21f mulcn cnmpt22f addcn
        cnsubsp2r crn wb cxp ffvelrn sylan adantrr simprl simprr wi lincmb01icc
        w3a ralrimivva fmpt2 sylib frn mulid2 mul02 oveq12d simpr oveq2d ax-1cn
        syl6eq simpl fvex ovmpt2a sylancl adantr 3eqtr4d sylancr mul01 mulid1
        cnsubsp2 mpbid syl5eqel sseldi sseli adantl addid1 subid1i subidi subcl
        syl6eleqr addid2 00id npcan jca jca31 ralrimiva mpbir2and ) AFDEUCZDGUD
        UENOFPPUFNZGUGNZOUAUHZQFNZUVBUUSUEZRZUVBSFNZUVBDUEZRZUIQUVBFNZQUUSUEZRZ
        SUVBFNZSUUSUEZRZUIZUIZUAQSUJNZUKAUUSDFGUAHAPULOZUVRGULOZEPPUGNZOZDPGUGN
        ZOZUUSUWBOUVRAUMUNZUWDHJIEDPPGUOUPIAUVJQEUEZDUEZQDUEZADUQZUVQUVQEVDZQUV
        QOZUVJUWFRAUVQGURZDVDZUWHAUVRUVSUWCUWLUWDHIDPGUVQUWKUSUWKUTVAVBUVQUWKDV
        CVEZAUVRUVRUWAUWIUWDUWDJEPPUVQUVQUSUSVAVBZUWJAVFUNUVQUVQQDEVGVBAUWEQDKV
        HVIZAUVMSEUEZDUEZSDUEZAUWHUWISUVQOZUVMUWQRUWMUWNUWSAVJUNUVQUVQSDEVGVBAU
        WPSDLVHVIZVKAFBCUVQUVQSCUHZVLNZBUHZEUEZTNZUXAUXCTNZVMNZDUEZVNUVAMABCUXG
        DPPPGUVQUVQUSUSUWDUWDUWDHABCUVQUVQUXGVNZUUTUVQVOVLUCZVPUEZVQVRUEZUGNZUU
        TPUGNZAUXIUUTUXKUGNZOZUXIUXMOZABCUXEUXFVMPPUXKUXKUXKVSUVQUVQVSUSUSUWDUW
        DAUXJVTOZUXKULOZUXRAUXJUXJUTZWAZUNUXJUXKUXKUTZWBVEZUYCABCUXBUXDTPPUXKUX
        KUXKVSUVQUVQVSUSUSUWDUWDUYCUYCAUUTULOZUXSUVQVSWCZBCUVQUVQUXBVNZUXMOUYFU
        XOOUYDAPPUMUMWDUNZUYCUYEAUVQWEVSQWEOZSWEOZUVQWEWCWFWGQSWHWIWTWJZUNZAUYF
        UXNUXMABCUBUXASUBUHZVLNZUXBPPPPUVQUVQUVQUSUSUWDUWDUWDUWDABCPPUVQUVQUSUS
        UWDUWDWKZUSUBUVQUYMWLUVTOAUBWMUNUYLUXASVLWNWOPUXLUUTUGWPWQZWRUVQUYFUUTU
        XKVSUXRVSUXKURRUYAUXJUXKVSUXJUXTWSUYBXAXBZXIXCAUYDUXSUYEBCUVQUVQUXDVNZU
        XMOUYQUXOOUYGUYCUYKAUYQUXNUXMABCUXCEPPPPUVQUVQUSUSUWDUWDUWDUWDABCPPUVQU
        VQUSUSUWDUWDXDZJXEUYOWRUVQUYQUUTUXKVSUYPXIXCUYPUYPUYCTUXKUXKUFNUXKUGNZO
        AUXJUXKUXTUYBXFUNZXGABCUXAUXCTPPUXKUXKUXKVSUVQUVQVSUSUSUWDUWDUYCUYCAUYD
        UXSUYEBCUVQUVQUXAVNZUXMOVUAUXOOUYGUYCUYKAVUAUXNUXMUYNUYOWRUVQVUAUUTUXKV
        SUYPXIXCAUYDUXSUYEBCUVQUVQUXCVNZUXMOVUBUXOOUYGUYCUYKAVUBUXNUXMUYRUYOWRU
        VQVUBUUTUXKVSUYPXIXCUYPUYPUYCUYTXGUYPUYPUYCVMUYSOAUXJUXKUXTUYBXHUNXGAUY
        DUXSUXIXJUVQWCZUYEUXPUXQXKUYGUYCAUVQUVQXLZUVQUXIVDZVUCAUXGUVQOZCUVQUKBU
        VQUKVUEAVUFBCUVQUVQAUXCUVQOZUXAUVQOZUIUIUXDUVQOZVUGVUHVUFAVUGVUIVUHAUWI
        VUGVUIUWNUVQUVQUXCEXMXNXOAVUGVUHXPAVUGVUHXQUYHUYIVUIVUGVUHXTVUFXRWFWGQS
        UXDUXCUXAXSWIVBYABCUVQUVQUXGUVQUXIUXIUTYBYCVUDUVQUXIYDVEUYKUVQUXIUUTUXK
        VSUYPUUAXCUUBUYOUUKIXEUUCAUVPUAUVQAUVBUVQOZUIZUVEUVHUVOVUKSUVBEUEZTNZQU
        VBTNZVMNZDUEZVULDUEZUVCUVDVUKVUOVULDVUKVUOVULQVMNZVULVUKVUMVULVUNQVMVUK
        VULVSOZVUMVULRVUKUVQVSVULUYJAUWIVUJVULUVQOUWNUVQUVQUVBEXMXNUUDZVULYEVEV
        UKUVBVSOZVUNQRVUJVVAAUVQVSUVBUYJUUEUUFZUVBYFVEYGVUKVUSVURVULRVUTVULUUGV
        EVIVHVUKVUJUWJUVCVUPRAVUJYHZVFBCUVBQUVQUVQUXHVUPFUXCUVBRZUXAQRZUIZUXGVU
        ODVVFUXEVUMUXFVUNVMVVFUXBSUXDVULTVVFUXBSQVLNSVVFUXAQSVLVVDVVEYHZYISYJUU
        HYKVVFUXCUVBEVVDVVEYLZVHYGVVFUXAQUXCUVBTVVGVVHYGYGVHMVUODYMYNYOVUKUWHUW
        IVUJUVDVUQRAUWHVUJUWMYPAUWIVUJUWNYPVVCUVQUVQUVBDEVGVBYQVUKUVFQVULTNZSUV
        BTNZVMNZDUEZUVGVUKVUJUWSUVFVVLRVVCVJBCUVBSUVQUVQUXHVVLFVVDUXASRZUIZUXGV
        VKDVVNUXEVVIUXFVVJVMVVNUXBQUXDVULTVVNUXBSSVLNQVVNUXASSVLVVDVVMYHZYISYJU
        UIYKVVNUXCUVBEVVDVVMYLZVHYGVVNUXASUXCUVBTVVOVVPYGYGVHMVVKDYMYNYOVUKVVKU
        VBDVUKVVKQUVBVMNZUVBVUKVVIQVVJUVBVMVUKVUSVVIQRVUTVULYFVEVUKVVAVVJUVBRVV
        BUVBYEVEYGVUKVVAVVQUVBRVVBUVBUULVEVIVHVIVUKUVKUVNVUKSUVBVLNZUWETNZUVBQT
        NZVMNZDUEZUWGUVIUVJVUKVWAQDVUKVWAQQVMNQVUKVVSQVVTQVMVUKVVSVVRQTNZQVUKUW
        EQVVRTAUWEQRVUJKYPYIVUKVVRVSOZVWCQRVUKSVSOZVVAVWDYJVVBSUVBUUJYRZVVRYSVE
        VIVUKVVAVVTQRVVBUVBYSVEYGUUMYKVHVUKUWJVUJUVIVWBRVFVVCBCQUVBUVQUVQUXHVWB
        FUXCQRZUXAUVBRZUIZUXGVWADVWIUXEVVSUXFVVTVMVWIUXBVVRUXDUWETVWIUXAUVBSVLV
        WGVWHYHZYIVWIUXCQEVWGVWHYLZVHYGVWIUXAUVBUXCQTVWJVWKYGYGVHMVWADYMYNYRAUV
        JUWGRVUJUWOYPYQVUKVVRUWPTNZUVBSTNZVMNZDUEZUWRUVLUVMVUKVWNSDVUKVWNVVRUVB
        VMNZSVUKVWLVVRVWMUVBVMVUKVWLVVRSTNZVVRVUKUWPSVVRTAUWPSRVUJLYPYIVUKVWDVW
        QVVRRVWFVVRYTVEVIVUKVVAVWMUVBRVVBUVBYTVEYGVUKVWEVVAVWPSRYJVVBSUVBUUNYRV
        IVHVUKUWSVUJUVLVWORVJVVCBCSUVBUVQUVQUXHVWOFUXCSRZVWHUIZUXGVWNDVWSUXEVWL
        UXFVWMVMVWSUXBVVRUXDUWPTVWSUXAUVBSVLVWRVWHYHZYIVWSUXCSEVWRVWHYLZVHYGVWS
        UXAUVBUXCSTVWTVXAYGYGVHMVWNDYMYNYRAUVMUWRRVUJUWTYPYQUUOUUPUUQUUR $.
        $( [7-Jun-2014] $) $( [15-Jun-2010] $)
    $}

    $( Reparametrization lemma.  The reparametrization of a path by any
       continuous map ` G : II --> II ` with ` G ( 0 ) = 0 ` and
       ` G ( 1 ) = 1 ` is path-homotopic to the original path.  (Contributed by
       Jeff Madsen, 15-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       8-Jun-2014.) $)
    reparpht $p |- ( ph -> ( F o. G ) ( ~=ph ` J ) F ) $=
      ( vx vy cfv cii co wcel wa cc0 wceq c1 syl3anc ccom cphtpc wbr ccn cphtpy
      c0 wne ctop wb isphtpc syl2anc iitop a1i cnco syl32anc wfun cicc wf iiuni
      cuni eqid cnf ffun syl 0elunit fvco3 fveq2d eqtrd 1elunit jca31 cmin cmul
      jca cv caddc cmpt2 reparphti ne0i mpbir2and ) ABCUAZBDUBLUCZVTMDUDNZOZBWB
      OZPQVTLZQBLZRZSVTLZSBLZRZPZPZVTBDUELNZUFUGZADUHOZWDWAWLWNPUIEFVTBDWBUJUKA
      WCWDWKAMUHOZWPWOCMMUDNOZWDWCWPAULUMZWREGFCBMMDUNUOFAWGWJAWEQCLZBLZWFABUPZ
      QSUQNZXBCURZQXBOZWEWTRAXBDUTZBURZXAAWPWOWDXFWREFBMDXBXEUSXEVAVBTXBXEBVCVD
      ZAWPWPWQXCWRWRGCMMXBXBUSUSVBTZXDAVEUMXBXBQBCVFTAWSQBHVGVHAWHSCLZBLZWIAXAX
      CSXBOZWHXJRXGXHXKAVIUMXBXBSBCVFTAXISBIVGVHVMVJAJKXBXBSKVNZVKNJVNZCLVLNXLX
      MVLNVONBLVPZWMOWNAJKBCXNDEFGHIXNVAVQWMXNVRVDVS $.
      $( [8-Jun-2014] $) $( [15-Jun-2010] $)
  $}

