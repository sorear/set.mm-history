$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Open sets of a metric space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d d x y z D $.  $d x y z X $.
    opnval.1 $e |- J = ( MetOpen ` D ) $.
    $( An open set is a subset of a metric space which includes a ball around
       each of its points.  Definition 1.3-2 of [Kreyszig] p. 18.  The object
       ` ( MetOpen `` D ) ` is the family of all open sets in the metric space
       determined by the metric ` D ` .  By ~ opntop , the open sets of a
       metric space form a topology ` J ` , whose base set is ` U. J ` by
       ~ uniopn2 . $)
    opnval $p |- ( D e. Met -> J = ( topGen ` ran ( ball ` D ) ) ) $=
      ( vd cme wcel copn cfv cbl crn ctg cv wceq fveq2 rneqd fveq2d df-opn fvex
      fvmpt syl5eq ) AEFBAGHAIHZJZKHZCDADLZIHZJZKHUCEGUDAMZUFUBKUGUEUAUDAINOPDQ
      UBKRST $.
      $( [12-Nov-2013] $) $( [1-Sep-2006] $)

    $( The set of open sets of a metric space is a topology.  Remark in
       [Kreyszig] p. 19.  This theorem connects the two concepts and makes
       available the theorems for topologies for use with metric spaces. $)
    opntop $p |- ( D e. Met -> J e. Top ) $=
      ( cme wcel cbl cfv crn ctg ctop opnval ctb blbas tgcl syl eqeltrd ) ADEZB
      AFGHZIGZJABCKQRLESJEAMRNOP $.
      $( [12-Nov-2013] $) $( [28-Aug-2006] $)
  $}

  ${
    $d x y z A $.  $d d x y z D $.  $d x y z X $.
    uniopn2.1 $e |- X = dom dom D $.
    uniopn2.2 $e |- J = ( MetOpen ` D ) $.
    $( The union of all open sets in a metric space is its underlying set. $)
    uniopn2 $p |- ( D e. Met -> X = U. J ) $=
      ( cme wcel cuni cbl cfv crn ctg opnval unieqd ctb blbas unitg syl unirnbl
      wceq 3eqtrrd ) AFGZBHAIJKZLJZHZUCHZCUBBUDABEMNUBUCOGUEUFTAPUCQRACDSUA $.
      $( [12-Nov-2013] $) $( [4-Sep-2006] $)

    $( The defining property of an open set of a metric space. $)
    isopn $p |- ( D e. Met -> ( A e. J <->
   ( A C_ X /\ A. x e. A E. y e. ran ( ball ` D ) ( x e. y /\ y C_ A ) ) ) ) $=
      ( cme wcel cbl cfv crn ctg cuni wss cv wa wrex wral opnval wb blbas eltg2
      eleq2d ctb syl unirnbl sseq2d anbi1d 3bitrd ) DIJZCEJCDKLMZNLZJZCUMOZPZAQ
      BQZJURCPRBUMSACTZRZCFPZUSRULEUNCDEHUAUEULUMUFJUOUTUBDUCABCUMUDUGULUQVAUSU
      LUPFCDFGUHUIUJUK $.
      $( [12-Nov-2013] $) $( [1-Sep-2006] $)

    $( The family of open sets of a metric space is a collection of subsets of
       the base set. $)
    opnfss $p |- ( D e. Met -> J C_ ~P X ) $=
      ( cme wcel cuni cpw pwuni uniopn2 pweqd syl5sseqr ) AFGZBHZIBCIBJNCOABCDE
      KLM $.
      $( [12-Nov-2013] $) $( [3-Sep-2006] $)

    $( The base set of a metric space is open.  Part of Theorem T1 of
       [Kreyszig] p. 19. $)
    opnm $p |- ( D e. Met -> X e. J ) $=
      ( cme wcel cuni uniopn2 ctop opntop eqid topopn syl eqeltrd ) AFGZCBHZBAB
      CDEIPBJGQBGABEKBQQLMNO $.
      $( [12-Nov-2013] $) $( [4-Sep-2006] $)

    $( A defining property of an open set of a metric space. $)
    isopn4 $p |- ( D e. Met -> ( A e. J <-> ( A C_ X /\
           A. x e. A E. y e. RR+ ( x ( ball ` D ) y ) C_ A ) ) ) $=
      ( vz cme wcel wss cv wa cbl cfv crn wrex wral co crp isopn blssex anassrs
      wb ssel2 sylan2 ralbidva pm5.32da bitrd ) DJKZCEKCFLZAMZIMZKUNCLNIDOPZQRZ
      ACSZNULUMBMUOTCLBUARZACSZNAICDEFGHUBUKULUQUSUKULNUPURACUKULUMCKZUPURUEZUL
      UTNUKUMFKVACFUMUFICDUMFBGUCUGUDUHUIUJ $.
      $( [12-Nov-2013] $) $( [5-May-2007] $)

    $( An open set of a metric space is a subspace of its base set. $)
    opnss $p |- ( ( D e. Met /\ A e. J ) -> A C_ X ) $=
      ( vx vy cme wcel wss cv wa cbl cfv crn wrex wral isopn simprbda ) BIJACJA
      DKGLHLZJUAAKMHBNOPQGARGHABCDEFST $.
      $( [4-Nov-2007] $) $( [3-Sep-2006] $)
  $}

  ${
    $d x D $.  $d x J $.  $d x K $.
    ismsg.j $e |- J = ( TopOpen ` K ) $.
    ismsg.d $e |- D = ( dist ` K ) $.
    $( Express the predicate " ` <. X , D >. ` is a metric space" with
       underlying set ` X ` and distance function ` D ` . $)
    ismsg $p |- ( K e. MetSp <->
                  ( K e. TopSp /\ D e. Met /\ J = ( MetOpen ` D ) ) ) $=
      ( vx cmt wcel ctps copn cfv wceq wa cme cts cds fveq2 syl6eqr ctop c0 w3a
      fveq2d eqeq12d df-ms elrab2 simpl cdm cbl crn ctg cvv df-opn dmmpt ssrab2
      cv crab eqsstri cbs cuni eqid istps simplbi eleq1 syl5ibcom imp 0opn 3syl
      elfvdm sseldi simpr 3jca 3simpb impbii bitri ) CGHCIHZBAJKZLZMZVOANHZVQUA
      ZFUOZOKZWAPKZJKZLVQFCIGWACLZWBBWDVPWEWBCOKBWACOQDRWEWCAJWEWCCPKAWACPQERUB
      UCFUDUEVRVTVRVOVSVQVOVQUFVRJUGZNAWFWAUHKUIUJKZUKHZFNUPNFNWGJFULUMWHFNUNUQ
      VRVPSHZTVPHAWFHVOVQWIVOBSHZVQWIVOWJCURKZBUSLWKBCWKUTDVAVBBVPSVCVDVEVPVFTA
      JVHVGVIVOVQVJVKVOVSVQVLVMVN $.
      $( [12-Nov-2013] $) $( [27-Aug-2006] $)
  $}

  ${
    $d x D $.  $d x J $.  $d x K $.
    msf.1 $e |- X = ( Base ` M ) $.
    msf.2 $e |- D = ( dist ` M ) $.
    $( The relation between the base set and the distance function. $)
    msdmdm $p |- ( M e. MetSp -> X = dom dom D ) $=
      ( cmt wcel cts cfv cuni copn cdm ctps wceq eqid ismsg simp3bi unieqd ctop
      cme wa simp1bi istps sylib simprd simp2bi uniopn2 syl 3eqtr4d ) BFGZBHIZJ
      ZAKIZJZCALLZUJUKUMUJBMGZATGZUKUMNZAUKBUKOZEPZQRUJUKSGZCULNZUJUPVAVBUAUJUP
      UQURUTUBCUKBDUSUCUDUEUJUQUOUNNUJUPUQURUTUFAUMUOUOOUMOUGUHUI $.
      $( [12-Nov-2013] $)

    $( Mapping of the distance function of a metric space. $)
    msf $p |- ( M e. MetSp -> D : ( X X. X ) --> RR ) $=
      ( cmt wcel cxp cr wf cdm cme ctps cts cfv copn wceq eqid ismsg simp2bi
      metf syl msdmdm xpeq12d feq2d mpbird ) BFGZCCHZIAJAKKZUIHZIAJZUGALGZUKUGB
      MGULBNOZAPOQAUMBUMRESTAUIUIRUAUBUGUHUJIAUGCUICUIABCDEUCZUNUDUEUF $.
      $( [12-Nov-2013] $) $( [30-Aug-2006] $)

    $( Closure of the distance function of a metric space. $)
    mscl $p |- ( ( M e. MetSp /\ A e. X /\ B e. X ) -> ( A D B ) e. RR ) $=
      ( cmt wcel cxp cr wf co msf fovrn syl3an1 ) DHIEEJKCLAEIBEIABCMKICDEFGNAB
      KEECOP $.
      $( [30-Aug-2006] $)
  $}

  ${
    $d f g u v w x y z A $.  $d f g u v w x y B $.  $d f g u v w x y z D $.
    $d x y P $.  $d x y R $.  $d u v w J $.
    opni.1 $e |- J = ( MetOpen ` D ) $.
    $( An open set of a metric space includes a ball around each of its
       points. $)
    opni $p |- ( ( D e. Met /\ A e. J /\ P e. A ) ->
                 E. x e. ran ( ball ` D ) ( P e. x /\ x C_ A ) ) $=
      ( vy cme wcel cv wss wa cbl cfv crn wrex cdm wral wi eqid rexbidv rcla4cv
      isopn wceq eleq1 anbi1d adantl syl6bi 3imp ) CHIZBEIZDBIZDAJZIZUMBKZLZACM
      NOZPZUJUKBCQQZKZGJZUMIZUOLZAUQPZGBRZLULURSZGABCEUSUSTFUCVEVFUTVDURGDBVADU
      DZVCUPAUQVGVBUNUOVADUMUEUFUAUBUGUHUI $.
      $( [12-Nov-2013] $) $( [3-Sep-2006] $)

    $( An open set of a metric space includes a ball around each of its
       points. $)
    opni2 $p |- ( ( D e. Met /\ A e. J /\ P e. A ) ->
                 E. x e. RR+ ( P ( ball ` D ) x ) C_ A ) $=
      ( vy cme wcel cv cbl cfv co wss crp wrex wa wral cdm eqid isopn4 simplbda
      wceq oveq1 sseq1d rexbidv rcla4cva sylan 3impa ) CHIZBEIZDBIZDAJZCKLZMZBN
      ZAOPZUJUKQGJZUMUNMZBNZAOPZGBRZULUQUJUKBCSSZNVBGABCEVCVCTFUAUBVAUQGDBURDUC
      ZUTUPAOVDUSUOBURDUMUNUDUEUFUGUHUI $.
      $( [12-Nov-2013] $) $( [2-May-2007] $)

    $( An open set of a metric space includes an arbitrarily small ball around
       each of its points. $)
    opni3 $p |- ( ( ( D e. Met /\ A e. J /\ P e. A ) /\ R e. RR+ ) ->
        E. x e. RR+ ( x < R /\ ( P ( ball ` D ) x ) C_ A ) ) $=
      ( vy cme wcel w3a crp wa cv cbl co wss wrex cdm wi cfv opni2 adantr simp1
      clt wbr eqid opnss sselda 3impa ssblex sstr expcom anim2d reximdv syl5com
      jca expr rexlimdv sylan mpd ) CIJZBFJZDBJZKZELJZMDHNZCOUAZPZBQZHLRZANZEUE
      UFZDVLVHPZBQZMZALRZVEVKVFHBCDFGUBUCVEVBDCSSZJZMZVFVKVQTVEVBVSVBVCVDUDVBVC
      VDVSVBVCMBVRDBCFVRVRUGZGUHUIUJUQVTVFMVJVQHLVTVFVGLJZVJVQTVTVFWBMMVMVNVIQZ
      MZALRVJVQACDEVGVRWAUKVJWDVPALVJWCVOVMWCVJVOVNVIBULUMUNUOUPURUSUTVA $.
      $( [12-Nov-2013] $) $( [20-Sep-2007] $)

    $( The balls of a metric space are open sets. $)
    blssopn $p |- ( D e. Met -> ran ( ball ` D ) C_ J ) $=
      ( cme wcel cbl cfv crn ctg ctb wss blbas bastg syl opnval sseqtr4d ) ADEZ
      AFGHZRIGZBQRJERSKALRMNABCOP $.
      $( [23-Dec-2013] $) $( [12-Sep-2006] $)

    $( The union of a collection of open sets of a metric space is open.
       Theorem T2 of [Kreyszig] p. 19. $)
    opnuni $p |- ( ( D e. Met /\ A C_ J ) ->
                 U. A e. J ) $=
      ( cme wcel ctop wss cuni opntop uniopn sylan ) BEFCGFACHAICFBCDJACKL $.
      $( [23-Dec-2013] $) $( [4-Sep-2006] $)

    $( The intersection of two open sets of a metric space is open. $)
    opnin $p |- ( ( D e. Met /\ A e. J /\ B e. J ) ->
               ( A i^i B ) e. J ) $=
      ( cme wcel ctop cin opntop inopn syl3an1 ) CFGDHGADGBDGABIDGCDEJABDKL $.
      $( [23-Dec-2013] $) $( [4-Sep-2006] $)
  $}

  ${
    opn0.1 $e |- J = ( MetOpen ` D ) $.
    $( The empty set is an open set of a metric space.  Part of Theorem T1 of
       [Kreyszig] p. 19. $)
    opn0 $p |- ( D e. Met -> (/) e. J ) $=
      ( cme wcel ctop c0 opntop 0opn syl ) ADEBFEGBEABCHBIJ $.
      $( [21-Feb-2008] $) $( [4-Sep-2006] $)

    $( A ball of a metric space is an open set. $)
    rnblopn $p |- ( ( D e. Met /\ B e. ran ( ball ` D ) ) -> B e. J ) $=
      ( cme wcel cbl cfv crn blssopn sselda ) BEFBGHICABCDJK $.
      $( [13-Nov-2007] $) $( [12-Sep-2006] $)
  $}

  ${
    $d r x y z D $.  $d r x y J $.  $d r x y z P $.  $d r y N $.  $d x y S $.
    $d r x y z R $.  $d r x y z X $.  $d z T $.
    blopn.1 $e |- X = dom dom D $.
    blopn.2 $e |- J = ( MetOpen ` D ) $.
    $( A ball of a metric space is an open set. $)
    blopn $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
                    ( P ( ball ` D ) R ) e. J ) $=
      ( cme wcel crp w3a cbl cfv co crn simp1 blelrn rnblopn syl2anc ) AHIZBEIZ
      CJIZKTBCALMZNZUCOIUDDITUAUBPABCEFQUDADGRS $.
      $( [12-Nov-2013] $) $( [9-Mar-2007] $)

    $( A neighborhood of a point defined in terms of balls. $)
    neibl $p |- ( ( D e. Met /\ P e. X ) -> ( N e. ( ( nei ` J ) ` { P } )
    <-> ( N C_ X /\ E. r e. RR+ ( P ( ball ` D ) r ) C_ N ) ) ) $=
      ( vy wcel wa cfv wss cv wrex crp wb adantr syl2anc wi cme csn cnei cbl co
      cuni ctop opntop uniopn2 eleq2d biimpa eqid isneip sseq2d w3a opni2 sstr2
      anbi1d com12 reximdv 3exp imp4a ad2antrr rexlimdv blopn blcntr wceq eleq2
      syl5com sseq1 anbi12d rcla4ev expr 3expia impbid pm5.32da 3bitr2d ) AUAJZ
      BEJZKZDBUBCUCLLJZDCUFZMZBINZJZWDDMZKZICOZKZDEMZWHKWJBFNZAUDLUEZDMZFPOZKVT
      CUGJZBWBJZWAWIQVRWOVSACHUHRVRVSWPVREWBBACEGHUIZUJUKBICDWBWBULUMSVTWJWCWHV
      RWJWCQVSVREWBDWQUNRURVTWJWHWNVTWJKZWHWNWRWGWNICVRWDCJZWGWNTTVSWJVRWSWEWFW
      NVRWSWEWFWNTVRWSWEUOWLWDMZFPOWFWNFWDABCHUPWFWTWMFPWTWFWMWLWDDUQUSUTVIVAVB
      VCVDVTWNWHTWJVTWMWHFPVRVSWKPJZWMWHTZVRVSXAUOWLCJZBWLJZXBABWKCEGHVEABWKEGV
      FXCXDWMWHWGXDWMKIWLCWDWLVGWEXDWFWMWDWLBVHWDWLDVJVKVLVMSVNVDRVOVPVQ $.
      $( [23-Dec-2013] $) $( [8-Nov-2007] $)

    $( The neighborhoods around a point ` P ` of a metric space are those
       subsets containing a ball around ` P ` .  Definition of neighborhood in
       [Kreyszig] p. 19. $)
    metnei $p |- ( ( D e. Met /\ P e. X ) -> ( ( nei ` J ) ` { P } ) = { x |
         ( x C_ X /\ E. r e. RR+ ( P ( ball ` D ) r ) C_ x ) } ) $=
      ( cme wcel wa cv wss cbl cfv co crp wrex csn cnei neibl abbi2dv ) BIJCEJK
      ALZEMCFLBNOPUCMFQRKACSDTOOBCDUCEFGHUAUB $.
      $( [12-Nov-2013] $) $( [8-Nov-2007] $)

    $( A ball around a point is a neighborhood of the point. $)
    blnei $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
              ( P ( ball ` D ) R ) e. ( ( nei ` J ) ` { P } ) ) $=
      ( vr cme wcel crp w3a cbl cfv co csn cnei wss cv wrex blssm oveq2 rcla4ev
      ssid wceq sseq1d mpan2 3ad2ant3 wa wb neibl 3adant3 mpbir2and ) AIJZBEJZC
      KJZLBCAMNZOZBPDQNNJZURERZBHSZUQOZURRZHKTZABCEFUAUPUNVDUOUPURURRZVDURUDVCV
      EHCKVACUEVBURURVACBUQUBUFUCUGUHUNUOUSUTVDUIUJUPABDUREHFGUKULUM $.
      $( [12-Nov-2013] $) $( [8-Nov-2007] $)

    $( Every ball around a limit point ` P ` of a subset ` S ` includes a
       member of ` S ` (even if ` P e/ S ` ). $)
    lpbl $p |- ( ( ( D e. Met /\ S C_ X /\ P e. ( ( limPt ` J ) ` S ) ) /\
           R e. RR+ ) -> E. x e. S x e. ( P ( ball ` D ) R ) ) $=
      ( wcel wss cfv wa cin c0 wne syl wceq syl2anc syl3anc cme clp w3a crp cbl
      co csn cdif wrex cnei wral simpl1 cuni ctop opntop simpl2 uniopn2 sseqtrd
      cv eqid lpss simpl3 sseldd eleqtrrd simpr blnei islp2 mpbid ineq1 rcla4va
      wb neeq1d wex elin eldifi anim2i ancomd sylbi eximi n0 df-rex 3imtr4i ) B
      UAJZEGKZCEFUBLLZJZUCZDUDJZMZCDBUELUFZECUGZUHZNZOPZAUSZWJJZAEUIZWIWJWKFUJL
      LZJZWOWLNZOPZAWRUKZWNWIWCCGJWHWSWCWDWFWHULZWICFUMZGWIWEXDCWIFUNJZEXDKZWEX
      DKWIWCXEXCBFIUOQZWIEGXDWCWDWFWHUPWIWCGXDRXCBFGHIUQQZURZEFXDXDUTZVASWCWDWF
      WHVBZVCZXHVDWGWHVEBCDFGHIVFTWIWFXBXKWIXEXFCXDJWFXBVKXGXIXLCEAFXDXJVGTVHXA
      WNAWJWRWOWJRWTWMOWOWJWLVIVLVJSWOWMJZAVMWOEJZWPMZAVMWNWQXMXOAXMWPWOWLJZMZX
      OWOWJWLVNXQWPXNXPXNWPWOEWKVOVPVQVRVSAWMVTWPAEWAWBQ $.
      $( [23-Dec-2013] $) $( [9-Nov-2007] $)

    blcld.3 $e |- S = { z e. X | ( P D z ) <_ R } $.
    $( A "closed ball" in a metric space is actually closed.  (Contributed by
       Mario Carneiro, 31-Dec-2013.) $)
    blcld $p |- ( ( D e. Met /\ P e. X /\ R e. RR ) -> S e. ( Clsd ` J ) ) $=
      ( vx wcel cr wceq co wbr syl3anc syl2anc cle wb vy cme w3a ccld cuni cdif
      cfv uniopn2 3ad2ant1 difeq1d cv wss wa wrex wral cbl simpl1 eldifi adantl
      cmin crp cc0 clt simpl2 metcl simpl3 resubcl wn eldif oveq2 breq1d elrab2
      simplbi2 con3d sylbi ltnle mpbird posdif mpbid elrp sylanbrc blopn blcntr
      imp cin c0 inss1 sseli blssm sseld wi simp11 3adant3 3ad2ant2 simp3 elbl2
      syl22anc ltsub13 caddc simp12 mettri3 syl13anc lesubadd ltletr exp3acom23
      mpd sylbid 3expia com23 mpdd syl5 inss2 simplbi simprbi lenlt syl5ib mpdi
      pm2.65d eq0rdv reldisj syl eleq2 sseq1 anbi12d rcla4ev syl12anc ralrimiva
      ctop opntop eltop2 eqeltrrd crab ssrab2 eqsstri syl5sseq eqid iscld2 ) BU
      BLZCGLZDMLZUCZEFUDUGLZFUEZEUFZFLZUUAGEUFZUUDFUUAGUUCEYRYSGUUCNYTBFGHIUHUI
      ZUJUUAUUFFLZUAUKZKUKZLZUUJUUFULZUMZKFUNZUAUUFUOZUUAUUNUAUUFUUAUUIUUFLZUMZ
      UUICUUIBOZDUTOZBUPUGOZFLZUUIUUTLZUUTUUFULZUUNUUQYRUUIGLZUUSVALZUVAYRYSYTU
      UPUQZUUPUVDUUAUUIGEURZUSZUUQUUSMLZVBUUSVCPZUVEUUQUURMLZYTUVIUUQYRYSUVDUVK
      UVFYRYSYTUUPVDUVHCUUIBGHVEQZYRYSYTUUPVFZUURDVGRUUQDUURVCPZUVJUUQUVNUURDSP
      ZVHZUUPUVPUUAUUPUVDUUIELZVHZUMUVPUUIGEVIUVDUVRUVPUVDUVOUVQUVQUVDUVOCAUKZB
      OZDSPZUVOAUUIGEUVSUUINUVTUURDSUVSUUICBVJVKJVLVMVNWDVOUSUUQYTUVKUVNUVPTUVM
      UVLDUURVPRVQUUQYTUVKUVNUVJTUVMUVLDUURVRRVSUUSVTWAZBUUIUUSFGHIWBQUUQYRUVDU
      VEUVBUVFUVHUWBBUUIUUSGHWCQUUQUUTEWEZWFNZUVCUUQKUWCUUQUUJUWCLZDCUUJBOZVCPZ
      UWEUUJUUTLZUUQUWGUWCUUTUUJUUTEWGWHUUQUWHUUJGLZUWGUUQUUTGUUJUUQYRUVDUVEUUT
      GULZUVFUVHUWBBUUIUUSGHWIQZWJUUQUWIUWHUWGUUAUUPUWIUWHUWGWKUUAUUPUWIUCZUWHU
      UIUUJBOZUUSVCPZUWGUWLYRUVEUVDUWIUWHUWNTYRYSYTUUPUWIWLZUUAUUPUVEUWIUWBWMUU
      PUUAUVDUWIUVGWNZUUAUUPUWIWOZUUJBUUIUUSGHWPWQUWLUWNDUURUWMUTOZVCPZUWGUWLUW
      MMLZUVKYTUWNUWSTUWLYRUVDUWIUWTUWOUWPUWQUUIUUJBGHVEQZUUAUUPUVKUWIUVLWMZUUA
      UUPYTUWIUVMWMZUWMUURDWRQUWLUWRUWFSPZUWSUWGWKZUWLUXDUURUWFUWMWSOSPZUWLYRYS
      UVDUWIUXFUWOYRYSYTUUPUWIWTZUWPUWQCUUIUUJBGHXAXBUWLUVKUWTUWFMLZUXDUXFTUXBU
      XAUWLYRYSUWIUXHUWOUXGUWQCUUJBGHVEQZUURUWMUWFXCQVQUWLYTUWRMLZUXHUXDUXEWKUX
      CUWLUVKUWTUXJUXBUXAUURUWMVGRUXIYTUXJUXHUCUWSUXDUWGDUWRUWFXDXEQXFXGXGXHXIX
      JXKUWEUUJELZUUQUWGVHZUWCEUUJUUTEXLWHUUQUXKUWIUXLUXKUWIUWFDSPZUWAUXMAUUJGE
      UVSUUJNUVTUWFDSUVSUUJCBVJVKJVLZXMUUQUWIUXKUXLUUAUUPUWIUXKUXLWKUXKUXMUWLUX
      LUXKUWIUXMUXNXNUWLUXHYTUXMUXLTUXIUXCUWFDXORXPXHXIXQXKXRXSUUQUWJUWDUVCTUWK
      UUTEGXTYAVSUUMUVBUVCUMKUUTFUUJUUTNUUKUVBUULUVCUUJUUTUUIYBUUJUUTUUFYCYDYEY
      FYGUUAFYHLZUUHUUOTYRYSUXOYTBFIYIUIZUAKUUFFYJYAVQYKUUAUXOEUUCULUUBUUETUXPU
      UAGEUUCEUWAAGYLGJUWAAGYMYNUUGYOEFUUCUUCYPYQRVQ $.
      $( [29-Jan-2014] $) $( [31-Dec-2013] $)

    $( The closure of an open ball in a metric space is contained in the
       corresponding closed ball.  (The converse is not, in general, true; for
       example, with the discrete metric, the closed ball of radius 1 is the
       whole space, but the open ball of radius 1 is just a point, whose
       closure is also a point.)  (Contributed by Mario Carneiro,
       31-Dec-2013.) $)
    blcls $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
                  ( ( cls ` J ) ` ( P ( ball ` D ) R ) ) C_ S ) $=
      ( cme wcel crp w3a cfv co wss cr wbr wa ctop ccld cbl ccl opntop 3ad2ant1
      rpre blcld syl3an3 cv cle crab wral blssm clt metcl 3expa 3adantl3 simpl3
      elbl wi syl ltle syl2anc expimpd sylbid ralrimiv ssrab sylanbrc syl6sseqr
      cuni eqid clsss2 syl3anc ) BKLZCGLZDMLZNZFUALZEFUBOLZCDBUCOPZEQWAFUDOOEQV
      OVPVSVQBFIUEUFVQVOVPDRLZVTDUGZABCDEFGHIJUHUIVRWACAUJZBPZDUKSZAGULZEVRWAGQ
      WFAWAUMWAWGQBCDGHUNVRWFAWAVRWDWALWDGLZWEDUOSZTWFWDBCDGHUTVRWHWIWFVRWHTZWE
      RLZWBWIWFVAVOVPWHWKVQVOVPWHWKCWDBGHUPUQURWJVQWBVOVPVQWHUSWCVBWEDVCVDVEVFV
      GWFAGWAVHVIJVJEWAFFVKZWLVLVMVN $.
      $( [31-Dec-2013] $)

    $( A smaller closed ball is contained in a larger open ball.  (Contributed
       by Mario Carneiro, 5-Jan-2014.) $)
    blsscls2 $p |- ( ( ( D e. Met /\ P e. X ) /\
                       ( R e. RR+ /\ T e. RR+ /\ R < T ) ) ->
                     S C_ ( P ( ball ` D ) T ) ) $=
      ( wcel wa crp clt wbr w3a co wi cr cme cv cle crab cbl cfv wral wss metcl
      simplr3 adantlr simplr1 rpre syl simplr2 lelttr exp3acom23 syl3anc mpd wb
      3expa simp2 elbl2 sylanr1 anassrs sylibrd ralrimiva rabss sylibr syl5eqss
      an4s ) BUALZCHLZMZDNLZFNLZDFOPZQZMZECAUBZBRZDUCPZAHUDZCFBUEUFRZKVSWBVTWDL
      ZSZAHUGWCWDUHVSWFAHVSVTHLZMZWBWAFOPZWEWHVQWBWISZVOVPVQVNWGUJWHWATLZDTLZFT
      LZVQWJSVNWGWKVRVLVMWGWKCVTBHIUIVAUKWHVOWLVOVPVQVNWGULDUMUNWHVPWMVOVPVQVNW
      GUOFUMUNWKWLWMQWBVQWIWADFUPUQURUSVNVRWGWEWIUTZVRVNVPWGWNVOVPVQVBVLVPVMWGW
      NVTBCFHIVCVKVDVEVFVGWBAHWDVHVIVJ $.
      $( [10-Jan-2014] $)
  $}

  ${
    $d x D $.  $d x P $.  $d x R $.  $d x S $.  $d x X $.
    blsscls.1 $e |- X = dom dom D $.
    blsscls.2 $e |- J = ( MetOpen ` D ) $.
    $( If two concentric balls have different radii, the closure of the smaller
       one is contained in the larger one.  (Contributed by Mario Carneiro,
       5-Jan-2014.) $)
    blsscls $p |- ( ( ( D e. Met /\ P e. X ) /\
                    ( R e. RR+ /\ S e. RR+ /\ R < S ) ) ->
            ( ( cls ` J ) ` ( P ( ball ` D ) R ) ) C_ ( P ( ball ` D ) S ) ) $=
      ( vx cme wcel wa crp clt wbr w3a cbl cfv co ccl cle crab eqid blcls 3expa
      cv wss 3ad2antr1 blsscls2 sstrd ) AJKZBFKZLZCMKZDMKZCDNOZPLBCAQRZSETRRZBI
      UFASCUAOIFUBZBDUQSUMUOUNURUSUGZUPUKULUNUTIABCUSEFGHUSUCZUDUEUHIABCUSDEFGH
      VAUIUJ $.
      $( [5-Jan-2014] $)
  $}

  ${
    $d g r s x C $.  $d g r s x D $.  $d g r s x J $.  $d g r s x K $.
    $d g r s x X $.  $d g r s x Y $.
    metequiv.1 $e |- X = dom dom C $.
    metequiv.2 $e |- Y = dom dom D $.
    metequiv.3 $e |- J = ( MetOpen ` C ) $.
    metequiv.4 $e |- K = ( MetOpen ` D ) $.
    $( One direction of ~ metequiv . $)
    metequivlem $p |- ( ( ( C e. Met /\ D e. Met ) /\ X = Y ) ->
          ( A. x e. X A. r e. RR+ E. s e. RR+
                ( x ( ball ` D ) s ) C_ ( x ( ball ` C ) r ) -> J C_ K ) ) $=
      ( wcel wa cv wss crp wral wi vg cme wceq cbl cfv wrex ssralv sstr2 reximi
      r19.37av syl ralimi r19.23v sylib ral2imi syl6com imdistand simprl sseq2d
      co ad2antll anbi1d sylibd wb isopn4 ad2antrr ad2antlr 3imtr4d ssrdv expr
      ) BUBNZCUBNZOZFGUCZAPZHPCUDUEUTZVOIPBUDUEUTZQZHRUFZIRSZAFSZDEQVMVNWAOZOZU
      ADEWCUAPZFQZVQWDQZIRUFZAWDSZOZWDGQZVPWDQZHRUFZAWDSZOZWDDNZWDENZWCWIWEWMOW
      NWCWEWHWMWAWEWHWMTZTVMVNWEWAVTAWDSWQVTAWDFUGVTWGWLAWDVTWFWLTZIRSWGWLTVSWR
      IRVSWFWKTZHRUFWRVRWSHRVPVQWDUHUIWFWKHRUJUKULWFWLIRUMUNUOUPVAUQWCWEWJWMWCF
      GWDVMVNWAURUSVBVCVKWOWIVDVLWBAIWDBDFJLVEVFVLWPWNVDVKWBAHWDCEGKMVEVGVHVIVJ
      $.
      $( [12-Nov-2013] $)

    $( The other direction of ~ metequiv . $)
    metequivlem2 $p |- ( ( ( C e. Met /\ D e. Met /\ J = K ) /\ x e. X ) ->
      A. r e. RR+ E. s e. RR+ ( x ( ball ` D ) s ) C_ ( x ( ball ` C ) r ) ) $=
      ( cme wcel cv wa cbl crp syl3anc wceq w3a cfv co wss simpl2 simpl1 simprl
      wrex simprr blopn simpl3 eleqtrd blcntr opni2 anassrs ralrimiva ) BNOZCNO
      ZDEUAZUBZAPZFOZQVBHPCRUCUDVBIPZBRUCUDZUEHSUIZISVAVCVDSOZVFVAVCVGQZQZUSVEE
      OVBVEOZVFURUSUTVHUFVIVEDEVIURVCVGVEDOURUSUTVHUGZVAVCVGUHZVAVCVGUJZBVBVDDF
      JLUKTURUSUTVHULUMVIURVCVGVJVKVLVMBVBVDFJUNTHVECVBEMUOTUPUQ $.
      $( [12-Nov-2013] $)

    $( Two ways of saying that two metrics generate the same topology.  Two
       metrics satisfying the right-hand side are said to be (topologically)
       equivalent.  (Contributed by Jeffrey Hankins, 21-Jun-2009.) $)
    metequiv $p |- ( ( C e. Met /\ D e. Met ) -> ( J = K <->
      ( X = Y /\ A. x e. X
          ( A. r e. RR+ E. s e. RR+
                ( x ( ball ` D ) s ) C_ ( x ( ball ` C ) r ) /\
            A. s e. RR+ E. r e. RR+
                ( x ( ball ` C ) r ) C_ ( x ( ball ` D ) s ) ) ) ) ) $=
      ( wcel wa wceq cv wss crp wral cme cbl cfv co wrex w3a cuni unieq uniopn2
      eqeqan12d 3impia metequivlem2 simpl2 simpl1 simpl3 eqcomd eleq2d syl31anc
      syl5ibr biimpa ralrimiva 3expia metequivlem wb raleq adantl ancom syl2anb
      jca wi eqcom sylbid anim12d r19.26 eqss 3imtr4g expimpd impbid ) BUANZCUA
      NZOZDEPZFGPZAQZHQCUBUCUDZWDIQBUBUCUDZRHSUEISTZWFWERISUEHSTZOZAFTZOZVSVTWB
      WKVSVTWBUFZWCWJVSVTWBWCWBWCWADUGZEUGZPDEUHVSVTFWMGWNBDFJLUICEGKMUIUJUSUKZ
      WLWIAFWLWDFNZOZWGWHABCDEFGHIJKLMULWQVTVSEDPWDGNZWHVSVTWBWPUMVSVTWBWPUNWQD
      EVSVTWBWPUOUPWLWPWRWLFGWDWOUQUTACBEDGFIHKJMLULURVIVAVIVBWAWCWJWBWAWCOZWGA
      FTZWHAFTZODERZEDRZOWJWBWSWTXBXAXCABCDEFGHIJKLMVCWSXAWHAGTZXCWCXAXDVDWAWHA
      FGVEVFWAVTVSOGFPXDXCVJWCVSVTVGFGVKACBEDGFIHKJMLVCVHVLVMWGWHAFVNDEVOVPVQVR
      $.
      $( [12-Nov-2013] $) $( [20-Jun-2009] $)
  $}

  ${
    $d m n x y D $.  $d m n x y J $.
    methausi.1 $e |- D e. Met $.
    methausi.2 $e |- J = ( MetOpen ` D ) $.
    $( The topology generated by a metric space is Hausdorff.  Remark in
       [Munkres] p. 126. $)
    methausi $p |- J e. Haus $=
      ( vx vy vm vn cdm wcel wceq cv w3a co c2 cin c0 crp mp3an1 syl2anc opntop
      cme cuni eqid uniopn2 ax-mp ctop wne cdiv cbl cfv wrex simp1 cr cc0 metcl
      clt wbr 3adant3 wb metgt0 biimp3a elrp sylanbrc 2pos elrpii rpdivcl blopn
      2re sylancl simp2 blcntr cle wa rehalfcl leid 3syl bl2in mp3anl1 syl22anc
      eleq2 ineq1 eqeq1d 3anbi13d ineq2 3anbi23d rcla42ev syl113anc ishausi ) E
      FGHBAIIZAUBJZWJBUCKCABWJWJUDZDUEUFWKBUGJCABDUAUFELZWJJZFLZWJJZWMWOUHZMZWM
      WMWOANZOUINZAUJUKZNZBJZWOWTXANZBJZWMXBJZWOXDJZXBXDPZQKZWMHLZJZWOGLZJZXJXL
      PZQKZMZGBULHBULWRWNWTRJZXCWNWPWQUMZWRWSRJZORJXQWRWSUNJZUOWSUQURZXSWNWPXTW
      QWKWNWPXTCWMWOAWJWLUPSZUSWNWPWQYAWKWNWPWQYAUTCWMWOAWJWLVASVBWSVCVDOVIVEVF
      WSOVGVJZWKWNXQXCCAWMWTBWJWLDVHSTWRWPXQXEWNWPWQVKZYCWKWPXQXECAWOWTBWJWLDVH
      STWRWNXQXFXRYCWKWNXQXFCAWMWTWJWLVLSTWRWPXQXGYDYCWKWPXQXGCAWOWTWJWLVLSTWRW
      NWPXQWTWTVMURZXIXRYDYCWNWPYEWQWNWPVNXTWTUNJYEYBWSVOWTVPVQUSWKWNWPXQYEVNXI
      CAWMWOWTWJWLVRVSVTXPXFXGXIMXFXMXBXLPZQKZMHGXBXDBBXJXBKZXKXFXOYGXMXJXBWMWA
      YHXNYFQXJXBXLWBWCWDXLXDKZXMXGYGXIXFXLXDWOWAYIYFXHQXLXDXBWEWCWFWGWHWI $.
      $( [12-Nov-2013] $) $( [9-Mar-2007] $)
  $}

  ${
    methaus.1 $e |- J = ( MetOpen ` D ) $.
    $( The topology generated by a metric space is Hausdorff.  Remark in
       [Munkres] p. 126. $)
    methaus $p |- ( D e. Met -> J e. Haus ) $=
      ( cme wcel cha cif copn cfv wceq fveq2 syl5eq eleq1d 0met elimel methausi
      c0 eqid dedth ) ADEZBFETAQGZHIZFEAQAUAJZBUBFUCBAHIUBCAUAHKLMUAUBAQDNOUBRP
      S $.
      $( [3-Dec-2007] $) $( [15-Mar-2007] $)
  $}

  ${
    $d r u x y z C $.  $d r y D $.  $d r u x y z J $.  $d r u x y z X $.
    $d r u x y z Y $.  $d x K $.
    subtopmet.1 $e |- D = ( C |` ( Y X. Y ) ) $.
    subtopmet.2 $e |- X = dom dom C $.
    subtopmet.3 $e |- J = ( MetOpen ` C ) $.
    subtopmet.4 $e |- K = ( MetOpen ` D ) $.
    $( Two alternate formulations of a subspace topology of a metric space
       topology.  (Contributed by Jeff Hankins, 21-Aug-2009.)  (The proof was
       shortened by Mario Carneiro, 5-Jan-2014.) $)
    subtopmet $p |- ( ( C e. Met /\ Y C_ X ) -> ( subSp ` <. Y , J >. ) = K )
        $=
      ( vu vy vr vz wcel wss wa wrex crp wi cme cop csubsp cfv cin wceq cdm cbl
      vx cv wral isopn4 simplbda adantlr inss1 ssralv ax-mp ssrin reximi ralimi
      co 3syl inss2 jctil sseq1 rexbidv raleqbi1dv anbi12d syl5ibrcom rexlimdva
      sseq2 cab cuni ctop opntop ad2antrr ssel2 w3a blopn eleq1a sylan2 anassrs
      3expa adantrd adantrr abssdv uniopn syl2anc oveq1 ineq1d rcla4cv ad2antll
      syl sseq1d ssel blcntr a1d reximdva ex sylan9r mpdd eleq2d rcla4ev sylcom
      ancld simprl sseld jcad elin sylan2br rexlimivw imp impbid1 eluniab ancom
      expr anass r19.41v rexbii bitr2i 3bitri exbii ineq1 eleq2 ceqsexv rexcom4
      wex ovex bitr3i anbi1i syl6bb eqrdv eqeq2d impbid wb simpr sylanbrc dmeqi
      cvv adantr rexbidva ralbidva pm5.32da cres metssba2 syl6eqr sseq2d anbi1d
      blres cxp 3bitr2d opnm ssexg syl2anr elsubsp metres syl5eqel eqid 3bitr4d
      id ) AUAOZFEPZQZUIFCUBUCUDZDUVCUIUJZKUJZFUEZUFZKCRZUVEBUGZUGZPZLUJZMUJZBU
      HUDVAZUVEPZMSRZLUVEUKZQZUVEUVDOZUVEDOZUVCUVIUVEFPZUVMUVNAUHUDZVAZFUEZUVEP
      ZMSRZLUVEUKZQZUWBUVRQUVSUVCUVIUWIUVCUVHUWIKCUVCUVFCOZQZUWIUVHUVGFPZUWEUVG
      PZMSRZLUVGUKZQUWKUWOUWLUWKUWDUVFPZMSRZLUVFUKZUWQLUVGUKZUWOUVAUWJUWRUVBUVA
      UWJUVFEPUWRLMUVFACEHIULUMUNUVGUVFPUWRUWSTUVFFUOUWQLUVGUVFUPUQUWQUWNLUVGUW
      PUWMMSUWDUVFFURUSUTVBUVFFVCVDUVHUWBUWLUWHUWOUVEUVGFVEUWGUWNLUVEUVGUVHUWFU
      WMMSUVEUVGUWEVKVFVGVHVIVJUVCUWIUVIUVCUWIQZNUJZUWDUFZMSRZLUVERZUXAFUEZUVEP
      ZQZNVLZVMZCOZUVEUXIFUEZUFZUVIUWTCVNOZUXHCPUXJUVAUXMUVBUWIACIVOZVPUWTUXGNC
      UVCUWBUXGUXACOZTUWHUVCUWBQZUXDUXOUXFUXPUXCUXOLUVEUVCUWBUVMUVEOZUXCUXOTZUW
      BUXQQZUVCUVMFOZUXRUVEFUVMVQZUVAUVBUXTUXRUVBUXTQZUVAUVMEOZUXRFEUVMVQZUVAUY
      CQUXBUXOMSUVAUYCUVNSOZUXBUXOTZUVAUYCUYEVRUWDCOUYFAUVMUVNCEHIVSUWDCUXAVTWM
      WCVJWAWBWAWBVJWDWEWFUXHCWGWHUWTKUVEUXKUWTUVFUVEOZUWFUVFUWDOZQZMSRZLUVERZU
      VFFOZQZUVFUXKOZUWTUYGUYMUWTUYGUYKUYLUWTUYGUVFUVNUWCVAZFUEZUVEPZUVFUYOOZQZ
      MSRZUYKUWTUYGUYQMSRZUYTUWHUYGVUATUVCUWBUWGVUALUVFUVEUVMUVFUFZUWFUYQMSVUBU
      WEUYPUVEVUBUWDUYOFUVMUVFUVNUWCWIZWJWNZVFWKWLUVCUWBUYGVUAUYTTZTUWHUWBUYGUY
      LUVCVUEUVEFUVFWOUVBUYLUVFEOZUVAVUEFEUVFWOUVAVUFVUEUVAVUFQUYQUYSMSUVAVUFUY
      EUYQUYSTUVAVUFUYEVRZUYQUYRVUGUYRUYQAUVFUVNEHWPWQXEWCWRWSWTWTWEXAUYGUYTUYK
      UYJUYTLUVFUVEVUBUYIUYSMSVUBUWFUYQUYHUYRVUDVUBUWDUYOUVFVUCXBVHVFXCWSXDUWTU
      VEFUVFUVCUWBUWHXFXGXHUYKUYLUYGUYJUYLUYGTZLUVEUYIVUHMSUWFUYHUYLUYGUYHUYLQU
      WFUVFUWEOUYGUVFUWDFXIUWEUVEUVFVQXJXPXKXKXLXMUYNUVFUXIOZUYLQUYMUVFUXIFXIVU
      IUYKUYLVUIUVFUXAOZUXGQZNYGUXBUXFVUJQZQZMSRZLUVERZNYGZUYKUXGNUVFXNVUKVUONV
      UKUXGVUJQUXDVULQZVUOVUJUXGXOUXDUXFVUJXQVUOUXCVULQZLUVERVUQVUNVURLUVEUXBVU
      LMSXRXSUXCVULLUVEXRXTYAYBUYKVUNNYGZLUVERVUPUYJVUSLUVEUYJVUMNYGZMSRVUSVUTU
      YIMSVULUYINUWDUVMUVNUWCYHUXBUXFUWFVUJUYHUXBUXEUWEUVEUXAUWDFYCWNUXAUWDUVFY
      DVHYEXSVUMMNSYFYIXSVUNLNUVEYFXTYAYJXTYKYLUVHUXLKUXICUVFUXIUFUVGUXKUVEUVFU
      XIFYCYMXCWHWSYNUVCUWBUVRUWHUXPUVQUWGLUVEUVCUWBUXQUVQUWGYOZUXSUVCUXTVVAUYA
      UVAUVBUXTVVAUYBUVAUVMEFUEOZVVAUYBUYCUXTVVBUYDUVBUXTYPUVMEFXIYQUVAVVBQUVPU
      WFMSUVAVVBUYEUVPUWFYOUVAVVBUYEVRUVOUWEUVEBAUVMUVNEFHGUUIWNWCUUAWAWBWAWBUU
      BUUCUVCUWBUVLUVRUVCFUVKUVEUVCFAFFUUJUUDZUGZUGUVKAEFHUUEUVJVVDBVVCGYRYRUUF
      UUGUUHUUKUVCUXMFYSOZUVTUVIYOUVAUXMUVBUXNYTUVBUVBECOVVEUVAUVBUUTACEHIUULFE
      CUUMUUNKUVEFCYSUUOWHUVCBUAOZUWAUVSYOUVAVVFUVBUVABVVCUAGAFUUPUUQYTLMUVEBDU
      VKUVKUURJULWMUUSYL $.
      $( [5-Jan-2014] $) $( [19-Aug-2009] $)
  $}


