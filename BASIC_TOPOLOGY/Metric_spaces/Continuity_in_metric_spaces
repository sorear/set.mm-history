$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Continuity in metric spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    metcnf.1 $e |- X = dom dom C $.
    metcnf.2 $e |- Y = dom dom D $.
    metcnf.3 $e |- J = ( MetOpen ` C ) $.
    metcnf.4 $e |- K = ( MetOpen ` D ) $.
    $( A continuous function at point ` P ` is a mapping (metric space version
       of ~ cnpf ). $)
    metcnpf $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\
                F e. ( ( J CnP K ) ` P ) ) -> F : X --> Y ) $=
      ( cme wcel w3a wf cuni ctop opntop uniopn2 ccnp co cfv wa 3ad2ant1 eleq2d
      3ad2ant2 biimpa 3adant2 3jca eqid cnpf sylan feq2d wceq feq3 syl sylan9bb
      wb 3adant3 adantr mpbird ) AMNZBMNZCGNZOZDCEFUAUBUCNZUDGHDPZEQZFQZDPZVFER
      NZFRNZCVINZOVGVKVFVLVMVNVCVDVLVEAEKSUEVDVCVMVEBFLSUGVCVEVNVDVCVEVNVCGVICA
      EGIKTZUFUHUIUJCDEFVIVJVIUKVJUKULUMVFVHVKUSZVGVCVDVPVEVCVHVIHDPZVDVKVCGVIH
      DVOUNVDHVJUOVQVKUSBFHJLTHVJVIDUPUQURUTVAVB $.
      $( [16-Dec-2007] $)

    $( A continuous function is a mapping (metric space version of ~ cnf ). $)
    metcnf $p |- ( ( C e. Met /\ D e. Met /\
                F e. ( J Cn K ) ) -> F : X --> Y ) $=
      ( cme wcel wf cuni ctop opntop eqid wb uniopn2 ccn co w3a id syl3an feq2d
      cnf wceq feq3 syl sylan9bb 3adant3 mpbird ) ALMZBLMZCDEUAUBMZUCFGCNZDOZEO
      ZCNZUNDPMUOEPMUPUPUTADJQBEKQUPUDCDEURUSURRUSRUGUEUNUOUQUTSUPUNUQURGCNZUOU
      TUNFURGCADFHJTUFUOGUSUHVAUTSBEGIKTGUSURCUIUJUKULUM $.
      $( [1-Aug-2007] $)

    $( A constant function is continuous (metric space version of
       ~ cnconst ). $)
    metcnconst $p |- ( ( ( C e. Met /\ D e. Met ) /\ ( B e. Y /\
             F : X --> { B } ) ) -> F e. ( J Cn K ) ) $=
      ( cme wcel wa wf ctop cuni opntop uniopn2 csn ccn co anim12i adantr feq2d
      eleq2d bi2anan9r biimpa eqid cnconst syl2anc ) BMNZCMNZOZAHNZGAUAZDPZOZOE
      QNZFQNZOZAFRZNZERZUQDPZOZDEFUBUCNUOVBUSUMUTUNVABEKSCFLSUDUEUOUSVGUNUPVDUM
      URVFUNHVCACFHJLTUGUMGVEUQDBEGIKTUFUHUIADEFVEVCVEUJVCUJUKUL $.
      $( [10-Aug-2007] $)
  $}

  ${
    $d w x y z F $.  $d w x y z C $.  $d w x y z D $.  $d w x y z X $.
    $d w x y z Y $.  $d w x y z P $.  $d w x y z J $.  $d w x y K $.
    metcn.1 $e |- X = dom dom C $.
    metcn.2 $e |- J = ( MetOpen ` C ) $.
    metcn.3 $e |- Y = dom dom D $.
    metcn.4 $e |- K = ( MetOpen ` D ) $.
    $( MAY BE REVISED - avoid funimass3 here to simplify metcnp2 , and also
       use iscnp2 for metcnp $)
    $( Lemma for ~ metcnp . $)
    metcnplem $p |- ( ( ( C e. Met /\ D e. Met ) /\ ( F : X --> Y /\ P e. X ) )
     -> ( A. y e. RR+ E. z e. RR+ ( P ( ball ` C ) z ) C_
    ( `' F " ( ( F ` P ) ( ball ` D ) y ) ) <-> A. y e. RR+
      E. z e. RR+ A. w e. X ( ( P C w ) < z ->
    ( ( F ` P ) D ( F ` w ) ) < y ) ) ) $=
      ( vx wcel wa co crp cme wf cv cbl cfv ccnv cima wss wrex clt wi wral wfun
      wbr wb cdm simplrl ffun syl simplll simplrr simprr blssm syl3anc wceq fdm
      sseqtr4d funimass5 syl2anc crab blval raleqdv oveq2 breq1d ralrab ffvelrn
      w3a syl6bb ex simpllr simprl elbl2 expr syl21anc syld imp imbi2d ralbidva
      3bitrd anassrs rexbidva ) DUAQZEUAQZRZJKGUBZFJQZRZRZFBUCZDUDUESZGUFFGUEZA
      UCZEUDUESZUGUHZBTUIFCUCZDSZWSUJUNZXAXEGUEZESXBUJUNZUKZCJULZBTUIATWRXBTQZR
      XDXKBTWRXLWSTQZXDXKUOWRXLXMRZRZXDXHXCQZCWTULZXGXPUKZCJULZXKXOGUMZWTGUPZUH
      XDXQUOXOWOXTWNWOWPXNUQZJKGURUSXOWTJYAXOWLWPXMWTJUHWLWMWQXNUTZWNWOWPXNVAZW
      RXLXMVBZDFWSJLVCVDXOWOYAJVEYBJKGVFUSVGCWTXCGVHVIXOWLWPXMXQXSUOYCYDYEWLWPX
      MVQZXQXPCFPUCZDSZWSUJUNZPJVJZULXSYFXPCWTYJPDFWSJLVKVLYIXGXPCPJYGXEVEYHXFW
      SUJYGXEFDVMVNVOVRVDXOXRXJCJXOXEJQZRXPXIXGXOYKXPXIUOZXOYKXHKQZYLXOWOYKYMUK
      YBWOYKYMJKXEGVPVSUSXOWMXLXAKQZYMYLUKWLWMWQXNVTWRXLXMWAXOWOWPYNYBYDJKFGVPV
      IWMXLRYNYMYLXHEXAXBKNWBWCWDWEWFWGWHWIWJWKWH $.
      $( [12-Nov-2013] $) $( [12-May-2007] $)

    $( Two ways to say a mapping from metric ` C ` to metric ` D ` is
       continuous at point ` P ` . $)
    metcnp $p |- ( ( C e. Met /\ D e. Met /\ P e. X ) ->
                ( F e. ( ( J CnP K ) ` P ) <->
          ( F : X --> Y /\ A. y e. RR+ E. z e. RR+
      A. w e. X ( ( P C w ) < z -> ( ( F ` P ) D ( F ` w ) ) < y ) ) ) ) $=
      ( vx wcel wa wi crp cme w3a ccnp co cfv wf cv clt wbr wral wrex cuni ctop
      opntop 3ad2ant1 3ad2ant2 wceq uniopn2 eleqtrd eqid cnpf ex syl3anc feq23d
      sylibrd pm4.71rd ccnv cima wss cbl wb adantr eleq2d biimpa 3adant2 iscnp2
      simp3 biantrurd simpll2 simpr simpl3 ffvelrn syl2anc simprr simprl blcntr
      blopn eleq2 imaeq2 sseq2d anbi2d rexbidv imbi12d rcla4v 3imp simpll1 sstr
      opni2 expcom reximdv syl5com 3exp imp4a rexlimdv syl mpd ralrimiva simp1r
      anassrs syl3an1 r19.29 simp1ll imass2 sstr2 syl5 sseq1 anbi12d expr syl9r
      rcla4ev 3expa rexlimdva ralrimiv impbida bitr3d simpl1 metcnplem syl22anc
      imp3a simpl2 3bitrd pm5.32da bitrd ) DUAQZEUAQZFJQZUBZGFHIUCUDUEQZJKGUFZY
      RRYSFCUGZDUDBUGZUHUIFGUEZYTGUEEUDAUGZUHUISCJUJBTUKATUJZRYQYRYSYQYRHULZIUL
      ZGUFZYSYQHUMQZIUMQZFUUEQZYRUUGSYNYOUUHYPDHMUNUOZYOYNUUIYPEIOUNUPZYQFJUUEY
      NYOYPVQYNYOJUUEUQYPDHJLMURZUOZUSUUHUUIUUJUBYRUUGFGHIUUEUUFUUEUTZUUFUTZVAV
      BVCYQJKUUEUUFGUUNYOYNKUUFUQYPEIKNOURUPVDZVEVFYQYSYRUUDYQYSRZYRUUGUUBYTQZF
      PUGZQZUUTGVGZYTVHZVIZRZPHUKZSZCIUJZRZFUUADVJUEUDZUVBUUBUUCEVJUEUDZVHZVIZB
      TUKZATUJZUUDUURUUHUUIUUJYRUVIVKYQUUHYSUUKVLYQUUIYSUULVLYQUUJYSYNYPUUJYOYN
      YPUUJYNJUUEFUUMVMVNVOVLPCFGHIUUEUUFUUOUUPVPVCUURUVHUVIUVOUURUUGUVHYQYSUUG
      UUQVNVRUURUVHUVOUURUVHRUVNATUURUVHUUCTQZUVNUURUVHUVPRZRZUVAUUTUVLVIZRZPHU
      KZUVNUVRUVKIQZUVHUUBUVKQZUWAUVRYOUUBKQZUVPUWBYNYOYPYSUVQVSZUURUWDUVQUURYS
      YPUWDYQYSVTZYNYOYPYSWAZJKFGWBWCVLZUURUVHUVPWDZEUUBUUCIKNOWGVCUURUVHUVPWEU
      VRYOUWDUVPUWCUWEUWHUWIEUUBUUCKNWFVCUWBUVHUWCUWAUVGUWCUWASCUVKIYTUVKUQZUUS
      UWCUVFUWAYTUVKUUBWHUWJUVEUVTPHUWJUVDUVSUVAUWJUVCUVLUUTYTUVKUVBWIWJWKWLWMW
      NWOVCUVRYNUWAUVNSYNYOYPYSUVQWPYNUVTUVNPHYNUUTHQZUVAUVSUVNYNUWKUVAUVSUVNSY
      NUWKUVAUBUVJUUTVIZBTUKUVSUVNBUUTDFHMWRUVSUWLUVMBTUWLUVSUVMUVJUUTUVLWQWSWT
      XAXBXCXDXEXFXIXGUURUVORZUVGCIUWMYTIQZUUSUVFUWMUWNUUSUBZUVNUVKYTVIZRZATUKZ
      UVFUWOUVOUWPATUKZUWRUURUVOUWNUUSXHUWMYOUWNUUSUWSYNYOYPYSUVOVSAYTEUUBIOWRX
      JUVNUWPATXKWCUWOYQUWRUVFSZYQYSUVOUWNUUSXLYNYPUWTYOYNYPRZUWQUVFATUXAUWQUVF
      SUVPUXAUVNUWPUVFUXAUVMUWPUVFSZBTYNYPUUATQZUVMUXBSUVMUWPUVJUVCVIZYNYPUXCUB
      ZUVFUWPUVLUVCVIUVMUXDUVKYTUVBXMUVJUVLUVCXNXOUXEUVJHQZFUVJQZUXDUVFSDFUUAHJ
      LMWGDFUUAJLWFUXFUXGUXDUVFUVEUXGUXDRPUVJHUUTUVJUQUVAUXGUVDUXDUUTUVJFWHUUTU
      VJUVCXPXQXTXRWCXSYAYBYIVLYBVOXEXFXBYCYDYEUURYNYOYSYPUVOUUDVKYNYOYPYSYFYNY
      OYPYSYJUWFUWGABCDEFGHIJKLMNOYGYHYKYLYM $.
      $( [12-Nov-2013] $) $( [11-May-2007] $)

    $( Two ways to say a mapping from metric ` C ` to metric ` D ` is
       continuous at point ` P ` .  The distance arguments are swapped compared
       to ~ metcnp (and Munkres' ~ metcn ) for compatibility with ~ df-lm .
       Definition 1.3-3 of [Kreyszig] p. 20. $)
    metcnp2 $p |- ( ( C e. Met /\ D e. Met /\ P e. X ) ->
                ( F e. ( ( J CnP K ) ` P ) <->
          ( F : X --> Y /\ A. y e. RR+ E. z e. RR+
      A. w e. X ( ( w C P ) < z -> ( ( F ` w ) D ( F ` P ) ) < y ) ) ) ) $=
      ( wcel co clt crp wa cme w3a ccnp cfv wf cv wi wral wrex metcnp wb metsym
      wceq 3expa 3adantl2 breq1d adantlr simpll2 simplr simpll3 ffvelrn syl2anc
      wbr adantll syl3anc imbi12d ralbidva rexbidv ralbidv pm5.32da bitrd ) DUA
      PZEUAPZFJPZUBZGFHIUCQUDPJKGUEZFCUFZDQZBUFZRVCZFGUDZVQGUDZEQZAUFZRVCZUGZCJ
      UHZBSUIZASUHZTVPVQFDQZVSRVCZWBWAEQZWDRVCZUGZCJUHZBSUIZASUHZTABCDEFGHIJKLM
      NOUJVOVPWIWQVOVPTZWHWPASWRWGWOBSWRWFWNCJWRVQJPZTZVTWKWEWMVOWSVTWKUKVPVOWS
      TVRWJVSRVLVNWSVRWJUMZVMVLVNWSXAFVQDJLULUNUOUPUQWTWCWLWDRWTVMWAKPZWBKPZWCW
      LUMVLVMVNVPWSURWTVPVNXBVOVPWSUSVLVMVNVPWSUTJKFGVAVBVPWSXCVOJKVQGVAVDWAWBE
      KNULVEUPVFVGVHVIVJVK $.
      $( [13-Nov-2013] $) $( [4-Jun-2007] $)

    $( Two ways to say a mapping from metric ` C ` to metric ` D ` is
       continuous.  Theorem 10.1 of [Munkres] p. 127.  The second biconditional
       argument says that for every positive "epsilon" ` y ` there is a
       positive "delta" ` z ` such that a distance less than delta in ` C `
       maps to a distance less than epsilon in ` D ` . $)
    metcn $p |- ( ( C e. Met /\ D e. Met ) ->
                ( F e. ( J Cn K ) <-> ( F : X --> Y /\
                A. x e. X A. y e. RR+ E. z e. RR+
      A. w e. X ( ( x C w ) < z -> ( ( F ` x ) D ( F ` w ) ) < y ) ) ) ) $=
      ( wcel wa co cv wral cme ccn wf clt wbr cfv wi wrex cuni ctop opntop eqid
      crp cnf 3expia syl2an wb uniopn2 feq23 sylibrd pm4.71rd ad2antrr ad2antlr
      wceq ccnp biimpa cncnp syl3anc raleqdv metcnp 3expa adantlr simplr bitr4d
      biantrurd ralbidva 3bitr2d pm5.32da bitrd ) EUAPZFUAPZQZGHIUBRPZJKGUCZWCQ
      WDASZDSZERCSUDUEWEGUFWFGUFFRBSUDUEUGDJTCUMUHBUMTZAJTZQWBWCWDWBWCHUIZIUIZG
      UCZWDVTHUJPZIUJPZWCWKUGWAEHMUKZFIOUKZWLWMWCWKGHIWIWJWIULZWJULZUNUOUPVTJWI
      VDZKWJVDWDWKUQWAEHJLMURZFIKNOURJKWIWJGUSUPZUTVAWBWDWCWHWBWDQZWCGWEHIVERUF
      PZAWITZXBAJTWHXAWLWMWKWCXCUQVTWLWAWDWNVBWAWMVTWDWOVCWBWDWKWTVFAGHIWIWJWPW
      QVGVHXAXBAJWIVTWRWAWDWSVBVIXAXBWGAJXAWEJPZQZXBWDWGQZWGWBXDXBXFUQZWDVTWAXD
      XGBCDEFWEGHIJKLMNOVJVKVLXEWDWGWBWDXDVMVOVNVPVQVRVS $.
      $( [13-Nov-2013] $) $( [15-May-2007] $)

    ${
      $d w x y z A $.  $d x y w z C $.  $d x y w z D $.  $d w z K $.
      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a continuous metric space function, with
         function arguments as in ~ metcnp . $)
      metcnpi $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( P C y ) < x -> ( ( F ` P ) D ( F ` y ) ) < A ) ) $=
        ( vz wcel co crp clt cme w3a ccnp cfv cv wbr wi wral wrex metcnp biimpa
        wa wf simprd wceq breq2 imbi2d ralbidv rexbidv rcla4cva sylan anasss )
        DUAQEUAQFJQUBZGFHIUCRUDQZCSQZFBUEZDRAUETUFZFGUDVFGUDERZCTUFZUGZBJUHZASU
        IZVCVDULZVGVHPUEZTUFZUGZBJUHZASUIZPSUHZVEVLVMJKGUMZVSVCVDVTVSULPABDEFGH
        IJKLMNOUJUKUNVRVLPCSVNCUOZVQVKASWAVPVJBJWAVOVIVGVNCVHTUPUQURUSUTVAVB $.
        $( [13-Nov-2013] $) $( [17-Dec-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a continuous metric space function, with
         swapped distance function arguments as in ~ metcnp2 . $)
      metcnpi2 $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( y C P ) < x -> ( ( F ` y ) D ( F ` P ) ) < A ) ) $=
        ( vz wcel co crp clt cme w3a ccnp cfv cv wbr wi wral wrex wa wf metcnp2
        biimpa simprd wceq breq2 imbi2d ralbidv rexbidv rcla4cva sylan anasss )
        DUAQEUAQFJQUBZGFHIUCRUDQZCSQZBUEZFDRAUETUFZVFGUDFGUDERZCTUFZUGZBJUHZASU
        IZVCVDUJZVGVHPUEZTUFZUGZBJUHZASUIZPSUHZVEVLVMJKGUKZVSVCVDVTVSUJPABDEFGH
        IJKLMNOULUMUNVRVLPCSVNCUOZVQVKASWAVPVJBJWAVOVIVGVNCVHTUPUQURUSUTVAVB $.
        $( [13-Nov-2013] $) $( [16-Dec-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a metric space function continuous at
         ` P ` .  A variation of ~ metcnpi2 with non-strict ordering. $)
      metcnpi3 $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( y C P ) <_ x -> ( ( F ` y ) D ( F ` P ) ) <_ A ) ) $=
        ( wcel crp wa wbr syl vz cme w3a ccnp co cfv clt wral wrex cle metcnpi2
        cv wi c2 cdiv cr cc0 rehalfcl rpgt0 wb halfpos2 mpbid sylanbrc ad2antrl
        rpre simpll1 simprr simpll3 metcl syl3anc halfpos lelttr exp3acom23 imp
        elrp syl31anc simpll2 cuni ctop opntop 3ad2ant1 3ad2ant2 uniopn2 eleq2d
        wf biimpa 3adant2 3jca eqid cnpf sylan wceq simpl1 simpl2 feq23d mpbird
        adantrr adantr ffvelrn syl2anc simplrr ltle imim12d ralimdva impr breq2
        anassrs imbi1d ralbidv rcla4ev expr rexlimdva mpd ) DUBPZEUBPZFJPZUCZGF
        HIUDUEUFPZCQPZRZRZBULZFDUEZUAULZUGSZYBGUFZFGUFZEUEZCUGSZUMZBJUHZUAQUIYC
        AULZUJSZYHCUJSZUMZBJUHZAQUIZUABCDEFGHIJKLMNOUKYAYKYQUAQYAYDQPZYKYQYAYRY
        KRRYDUNUOUEZQPZYCYSUJSZYNUMZBJUHZYQYRYTYAYKYRYSUPPZUQYSUGSZYTYRYDUPPZUU
        DYDVEZYDURZTYRUQYDUGSZUUEYDUSZYRUUFUUIUUEUTUUGYDVATVBYSVOVCVDYAYRYKUUCY
        AYRRYJUUBBJYAYRYBJPZYJUUBUMYAYRUUKRZRZUUAYEYIYNUUMYCUPPZUUDUUFYSYDUGSZU
        UAYEUMZUUMXNUUKXPUUNXNXOXPXTUULVFYAYRUUKVGZXNXOXPXTUULVHZYBFDJLVIVJUUMU
        UFUUDYRUUFYAUUKUUGVDZUUHTUUSYRUUOYAUUKYRUUIUUOUUJYRUUFUUIUUOUTUUGYDVKTV
        BVDUUNUUDUUFUCZUUOUUPUUTUUAUUOYEYCYSYDVLVMVNVPUUMYHUPPZCUPPZYIYNUMUUMXO
        YFKPZYGKPZUVAXNXOXPXTUULVQUUMJKGWEZUUKUVCYAUVEUULXQXRUVEXSXQXRRZUVEHVRZ
        IVRZGWEZXQHVSPZIVSPZFUVGPZUCXRUVIXQUVJUVKUVLXNXOUVJXPDHMVTWAXOXNUVKXPEI
        OVTWBXNXPUVLXOXNXPUVLXNJUVGFDHJLMWCZWDWFWGWHFGHIUVGUVHUVGWIUVHWIWJWKUVF
        JKUVGUVHGUVFXNJUVGWLXNXOXPXRWMUVMTUVFXOKUVHWLXNXOXPXRWNEIKNOWCTWOWPWQWR
        ZUUQJKYBGWSWTUUMUVEXPUVDUVNUURJKFGWSWTYFYGEKNVIVJUUMXSUVBXQXRXSUULXACVE
        TYHCXBWTXCXGXDXEYPUUCAYSQYLYSWLZYOUUBBJUVOYMUUAYNYLYSYCUJXFXHXIXJWTXKXL
        XM $.
        $( [13-Nov-2013] $) $( [16-Dec-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a metric space function continuous at
         ` P ` .  A variation of ~ metcnpi with non-strict ordering. $)
      metcnpi4 $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( P C y ) <_ x -> ( ( F ` P ) D ( F ` y ) ) <_ A ) ) $=
        ( wcel co crp wa cle cme w3a ccnp cfv cv wbr wi wral wrex metcnpi3 wceq
        simpll1 simpr simpll3 metsym syl3anc breq1d simpll2 wf cuni ctop opntop
        3ad2ant1 3ad2ant2 uniopn2 eleq2d biimpa 3adant2 3jca eqid cnpf sylan wb
        feq23d adantr mpbird adantrr ffvelrn syl2anc imbi12d ralbidva rexbidv
        simpl3 mpbid ) DUAPZEUAPZFJPZUBZGFHIUCQUDPZCRPZSZSZBUEZFDQZAUEZTUFZWMGU
        DZFGUDZEQZCTUFZUGZBJUHZARUIFWMDQZWOTUFZWRWQEQZCTUFZUGZBJUHZARUIABCDEFGH
        IJKLMNOUJWLXBXHARWLXAXGBJWLWMJPZSZWPXDWTXFXJWNXCWOTXJWEXIWGWNXCUKWEWFWG
        WKXIULWLXIUMWEWFWGWKXIUNWMFDJLUOUPUQXJWSXECTXJWFWQKPZWRKPZWSXEUKWEWFWGW
        KXIURWLJKGUSZXIXKWHWIXMWJWHWISXMHUTZIUTZGUSZWHHVAPZIVAPZFXNPZUBWIXPWHXQ
        XRXSWEWFXQWGDHMVBVCWFWEXRWGEIOVBVDWEWGXSWFWEWGXSWEJXNFDHJLMVEZVFVGVHVIF
        GHIXNXOXNVJXOVJVKVLWHXMXPVMWIWHJKXNXOGWEWFJXNUKWGXTVCWFWEKXOUKWGEIKNOVE
        VDVNVOVPVQZJKWMGVRVLWLXLXIWLXMWGXLYAWEWFWGWKWCJKFGVRVSVOWQWREKNUOUPUQVT
        WAWBWD $.
        $( [13-Nov-2013] $) $( [17-Dec-2007] $)

      $( Epsilon-delta property of a continuous metric space function. $)
      metcni $p |- ( ( ( C e. Met /\ D e. Met /\
                  F e. ( J Cn K ) ) /\
           ( P e. X /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( P C y ) < x -> ( ( F ` P ) D ( F ` y ) ) < A ) ) $=
        ( cme wcel co cfv syl ccn w3a crp wa ccnp cv clt wbr wral simpl1 simpl2
        wi wrex simprl ctop cuni opntop simpl3 wceq uniopn2 eleqtrd eqid cncnpi
        syl22anc simprr metcnpi syl32anc ) DPQZEPQZGHIUARQZUBZFJQZCUCQZUDZUDZVH
        VIVLGFHIUERSQZVMFBUFZDRAUFUGUHFGSVQGSERCUGUHULBJUIAUCUMVHVIVJVNUJZVHVIV
        JVNUKZVKVLVMUNZVOHUOQZIUOQZVJFHUPZQVPVOVHWAVRDHMUQTVOVIWBVSEIOUQTVHVIVJ
        VNURVOFJWCVTVOVHJWCUSVRDHJLMUTTVAFGHIWCIUPZWCVBWDVBVCVDVKVLVMVEABCDEFGH
        IJKLMNOVFVG $.
        $( [13-Nov-2013] $) $( [3-Aug-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a continuous metric space function. $)
      metcni2 $p |- ( ( ( C e. Met /\ D e. Met /\
                  F e. ( J Cn K ) ) /\
           ( P e. X /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( P C y ) <_ x -> ( ( F ` P ) D ( F ` y ) ) <_ A ) ) $=
        ( cme wcel co cfv syl ccn w3a crp wa ccnp cv cle wbr wral simpl1 simpl2
        wi wrex simprl ctop cuni opntop simpl3 wceq uniopn2 eleqtrd eqid cncnpi
        syl22anc simprr metcnpi4 syl32anc ) DPQZEPQZGHIUARQZUBZFJQZCUCQZUDZUDZV
        HVIVLGFHIUERSQZVMFBUFZDRAUFUGUHFGSVQGSERCUGUHULBJUIAUCUMVHVIVJVNUJZVHVI
        VJVNUKZVKVLVMUNZVOHUOQZIUOQZVJFHUPZQVPVOVHWAVRDHMUQTVOVIWBVSEIOUQTVHVIV
        JVNURVOFJWCVTVOVHJWCUSVRDHJLMUTTVAFGHIWCIUPZWCVBWDVBVCVDVKVLVMVEABCDEFG
        HIJKLMNOVFVG $.
        $( [13-Nov-2013] $) $( [14-Dec-2007] $)
    $}

    $( Two ways to express that ` F ` is continuous at ` P ` for metric
       spaces.  Proposition 14-4.2 of [Gleason] p. 240. $)
    metcnp3 $p |- ( ( C e. Met /\ D e. Met /\ P e. X )
    -> ( F e. ( ( J CnP K ) ` P ) <-> ( F : X --> Y /\
    A. y e. RR+ E. z e. RR+ ( F " ( P ( ball ` C ) z ) )
    C_ ( ( F ` P ) ( ball ` D ) y ) ) ) ) $=
      ( vx wcel co cfv crp wa cme w3a ccnp wf cv clt wbr wral wrex cbl cima wss
      wi metcnp wb ccnv wfun cdm simplrl ffun syl simplll simplrr simpr syl3anc
      blssm wceq fdm sseqtr4d funimass3 syl2anc ralbidv metcnplem bitrd ancom2s
      rexbidva anassrs pm5.32da 3impa bitr4d ) CUAPZDUAPZEIPZUBFEGHUCQRPIJFUDZE
      OUEZCQBUEZUFUGEFRZWEFRDQAUEZUFUGUMOIUHBSUIASUHZTZWDFEWFCUJRQZUKWGWHDUJRQZ
      ULZBSUIZASUHZTZABOCDEFGHIJKLMNUNWAWBWCWPWJUOWAWBTZWCTWDWOWIWQWCWDWOWIUOZW
      QWDWCWRWQWDWCTZTZWOWKFUPWLUKULZBSUIZASUHWIWTWNXBASWTWMXABSWTWFSPZTZFUQZWK
      FURZULWMXAUOXDWDXEWQWDWCXCUSZIJFUTVAXDWKIXFXDWAWCXCWKIULWAWBWSXCVBWQWDWCX
      CVCWTXCVDCEWFIKVFVEXDWDXFIVGXGIJFVHVAVIWKWLFVJVKVPVLABOCDEFGHIJKLMNVMVNVO
      VQVRVSVT $.
      $( [13-Nov-2013] $) $( [17-May-2007] $)
  $}

  ${
    $d f w x y z B $.  $d f w x y z C $.  $d f w x y z D $.  $d f w x y z J $.
    $d f w x y K $.  $d f w x y L $.
    metcnco.1 $e |- J = ( MetOpen ` B ) $.
    metcnco.2 $e |- K = ( MetOpen ` C ) $.
    metcnco.3 $e |- L = ( MetOpen ` D ) $.
    $( Composition of two continuous functions (metric space version of
       ~ cnco ). $)
    metcnco $p |- ( ( ( B e. Met /\ C e. Met /\ D e. Met ) /\ (
        F e. ( J Cn K ) /\ G e. ( K Cn L ) ) ) -> ( G o. F ) e. ( J Cn L ) ) $=
      ( cme wcel ctop ccn co wa ccom opntop cnco syl3anl ) ALMFNMBLMGNMCLMHNMDF
      GOPMEGHOPMQEDRFHOPMAFISBGJSCHKSDEFGHTUA $.
      $( [6-Aug-2007] $)

    $( Subset relationship for continuity of metric spaces. $)
    metcnss $p |- ( ( ( B e. Met /\ C e. Met /\ D e. Met ) /\ C C_ D ) ->
      ( J Cn K ) C_ ( J Cn L ) ) $=
      ( vx vw vz vy wcel wa co cdm cv wral crp vf cme w3a wss ccn wf clt wbr wi
      cfv wrex wb cop wfun wceq cxp simpll3 eqid metf ffun 3syl ffvelrn adantrr
      cr simplr adantrl opelxpi syl2anc adantl simpll2 eleqtrrd funssfv syl3anc
      fdm df-ov 3eqtr4g breq1d imbi2d anassrs ralbidva rexbidv ralbidv pm5.32da
      dmss fss expcom anim1d sylbird simpl1 simpl2 metcn simpl3 3imtr4d ssrdv )
      AUBNZBUBNZCUBNZUCZBCUDZOZUADEUEPZDFUEPZWTAQQZBQZQZUARZUFZJRZKRZAPLRUGUHZX
      HXFUJZXIXFUJZBPZMRZUGUHZUIZKXCSZLTUKZMTSZJXCSZOZXCCQZQZXFUFZXJXKXLCPZXNUG
      UHZUIZKXCSZLTUKZMTSZJXCSZOZXFXANZXFXBNZWTYAXGYKOYLWTXGYKXTWTXGOZYJXSJXCYO
      XHXCNZOZYIXRMTYQYHXQLTYQYGXPKXCYOYPXIXCNZYGXPULZWTXGYPYROZYSWTXGYTOZOZYFX
      OXJUUBYEXMXNUGUUBXKXLUMZCUJZUUCBUJZYEXMUUBCUNZWSUUCXDNUUDUUEUOUUBWQYCYCUP
      ZVDCUFUUFWOWPWQWSUUAUQCYCYCURZUSUUGVDCUTVAWRWSUUAVEUUBUUCXEXEUPZXDUUAUUCU
      UINZWTUUAXKXENZXLXENZUUJXGYPUUKYRXCXEXHXFVBVCXGYRUULYPXCXEXIXFVBVFXKXLXEX
      EVGVHVIUUBWPUUIVDBUFXDUUIUOWOWPWQWSUUAVJBXEXEURZUSUUIVDBVNVAVKUUCCBVLVMXK
      XLCVOXKXLBVOVPVQVRVSVSVTWAWBVTWCWTXGYDYKWSXGYDUIZWRWSXDYBUDXEYCUDZUUNBCWD
      XDYBWDXGUUOYDXCXEYCXFWEWFVAVIWGWHWTWOWPYMYAULWOWPWQWSWIZWOWPWQWSWJJMLKABX
      FDEXCXEXCURZGUUMHWKVHWTWOWQYNYLULUUPWOWPWQWSWLJMLKACXFDFXCYCUUQGUUHIWKVHW
      MWN $.
      $( [13-Nov-2013] $) $( [29-May-2007] $)
  $}

  ${
    $d w x y z C $.  $d w x y z D $.  $d w x y z F $.  $d w x y z B $.
    $d w x y z J $.  $d w x y z K $.  $d w x y L $.  $d w x y z X $.
    metcnss2.1 $e |- X = dom dom B $.
    metcnss2.2 $e |- J = ( MetOpen ` B ) $.
    metcnss2.3 $e |- K = ( MetOpen ` C ) $.
    metcnss2.4 $e |- L = ( MetOpen ` D ) $.
    $( Subset relationship for continuity of metric spaces. $)
    metcnss2 $p |- ( ( ( B e. Met /\ C e. Met /\ D e. Met ) /\ ( B C_ C
                 /\ F e. ( K Cn L ) ) ) -> ( F |` X ) e. ( J Cn L ) ) $=
      ( vx vw vz vy wcel co wa wral cme w3a wss ccn cres cdm wf clt wbr cfv crp
      cv wi wrex dmss syl adantl syl5eqss fssres expcom ssralv adantr wfun wceq
      wb cop cxp cr simpll2 eqid metf ffun 3syl simplr opelxpi simpll1 eleqtrrd
      funssfv syl3anc df-ov 3eqtr4g breq1d bicomd oveq12 syl2an imbi12d anassrs
      fdm fvres ralbidva sylibrd reximdv ralimdv ralimdva anim12d simpl2 simpl3
      syld metcn syl2anc simpl1 3imtr4d impr ) AUAQZBUAQZCUAQZUBZABUCZDFGUDRQZD
      HUEZEGUDRQZXGXHSZBUFZUFZCUFUFZDUGZMULZNULZBRZOULZUHUIZXQDUJZXRDUJZCRZPULZ
      UHUIZUMZNXNTZOUKUNZPUKTZMXNTZSZHXOXJUGZXQXRARZXTUHUIZXQXJUJZXRXJUJZCRZYEU
      HUIZUMZNHTZOUKUNZPUKTZMHTZSZXIXKXLXPYMYKUUDXLHXNUCZXPYMUMXLHAUFZUFZXNIXHU
      UHXNUCZXGXHUUGXMUCUUIABUOUUGXMUOUPUQURZXPUUFYMXNXOHDUSUTUPXLYKYJMHTZUUDXL
      UUFYKUUKUMUUJYJMHXNVAUPXLYJUUCMHXLXQHQZSZYIUUBPUKUUMYHUUAOUKUUMYHYGNHTZUU
      AUUMUUFYHUUNUMXLUUFUULUUJVBYGNHXNVAUPUUMYTYGNHXLUULXRHQZYTYGVEXLUULUUOSZS
      ZYOYAYSYFUUQYAYOUUQXSYNXTUHUUQXQXRVFZBUJZUURAUJZXSYNUUQBVCZXHUURUUGQUUSUU
      TVDUUQXEXNXNVGZVHBUGUVAXDXEXFXHUUPVIBXNXNVJZVKUVBVHBVLVMXGXHUUPVNUUQUURHH
      VGZUUGUUPUURUVDQXLXQXRHHVOUQUUQXDUVDVHAUGUUGUVDVDXDXEXFXHUUPVPAHIVKUVDVHA
      WHVMVQUURBAVRVSXQXRBVTXQXRAVTWAWBWCUUQYRYDYEUHUUPYRYDVDZXLUULYPYBVDYQYCVD
      UVEUUOXQHDWIXRHDWIYPYBYQYCCWDWEUQWBWFWGWJWKWLWMWNWRWOXLXEXFXIYLVEXDXEXFXH
      WPXDXEXFXHWQZMPONBCDFGXNXOUVCKXOVJZLWSWTXLXDXFXKUUEVEXDXEXFXHXAUVFMPONACX
      JEGHXOIJUVGLWSWTXBXC $.
      $( [13-Nov-2013] $) $( [23-Aug-2007] $)
  $}

  ${
    metidcn.1 $e |- X = dom dom C $.
    metidcn.3 $e |- J = ( MetOpen ` C ) $.
    metidcn.4 $e |- K = ( MetOpen ` D ) $.
    $( The identity function is continuous (metric space version of
       ~ idcn ). $)
    metidcn $p |- ( ( C e. Met /\ D e. Met /\ C C_ D ) ->
       ( _I |` X ) e. ( J Cn K ) ) $=
      ( cme wcel wss w3a ccn co cid cres simp1 simp2 simp3 metcnss cuni uniopn2
      syl31anc reseq2d ctop opntop eqid idcn syl eqeltrd 3ad2ant1 sseldd ) AIJZ
      BIJZABKZLZCCMNZCDMNZOEPZUPUMUMUNUOUQURKUMUNUOQZUTUMUNUORUMUNUOSAABCCDGGHT
      UCUMUNUSUQJUOUMUSOCUAZPZUQUMEVAOACEFGUBUDUMCUEJVBUQJACGUFCVAVAUGUHUIUJUKU
      L $.
      $( [13-Nov-2013] $) $( [23-Aug-2007] $)
  $}

  ${
    $d b c d s t u v w x y z M $.  $d b c d s t u v x y z N $.
    $d a r s t u v x y z R $.  $d a r s t u v x y z S $.
    $d a r s t u v x y z U $.  $d a b c d r s t u v w x y z X $.
    $d a b c d r s t u v x y z Y $.
    txmet.1 $e |- R = ( MetOpen ` M ) $.
    txmet.2 $e |- S = ( MetOpen ` N ) $.
    txmet.3 $e |- X = dom dom M $.
    txmet.4 $e |- Y = dom dom N $.
    txmet.5 $e |- M e. Met $.
    txmet.6 $e |- N e. Met $.
    txmet.7 $e |- U = ( b e. ( X X. Y ) , c e. ( X X. Y ) |->
                        sup ( { ( ( 1st ` b ) M ( 1st ` c ) ) ,
                          ( ( 2nd ` b ) N ( 2nd ` c ) ) } , RR , < ) ) $.
    $( The product of two metric space topologies, in terms of the sup norm.
       (Contributed by Jeff Madsen, 11-Jun-2010.)  (Revised by Mario Carneiro,
       5-Jan-2014.) $)
    txmet $p |- ( R tX S ) = ( MetOpen ` U ) $=
      ( wceq co wcel wa vz vu vv vx vy va vr vs vt vw cv cxp ctg cfv cbl wss wi
      wrex wral cuni cme ctop opntop ax-mp uniopn2 mp2an 3eqtrri eqeq1 2rexbidv
      ctb vex elab crp cop wex elxp opnss mpan ssel2 sylan ad2ant2r ad2ant2l wb
      isopn4 simprbi oveq1 sseq1d rexbidv rcla4cva cle wbr jctl ad2antrr adantl
      cif rpre syl2an ssbl syl121anc sstr2 syl c2nd c1st clt elxp6 anbi1i metcl
      cr mp3an1 anim12i ltnle ifbid ifnot syl6eq breq1d maxlt bitrd 3expa an32s
      an4s wn pm5.32da syl5bi opelxpi elbl op1st eqcomi op2nd eqid metxpdval ex
      cdm adantr adantlr anbi12d anbi2d imp syl5ibrcom blssm wrel cab crn txval
      ctx copn txunii unieqi txbas unitg eleq2i reeanv ad2antrl simprl ad2antlr
      ifcl min1 simprr min2 anass bitri w3a ancoms 3adant3 biimpd syl6ib anim2d
      an4 metxp cpr csup ltso supex dmmpt2 dmxpid eqtr2i pm5.32d adantll syl5bb
      dmeqi 3imtr4d sylan2 ssrdv xpss12 syl2im syl2and impr oveq2 rcla4ev exp32
      syl2anc rexlimdvv sylan2br syl22anc com23 imp3a blssex mpbird eleq2 sseq2
      exlimdvv imbi12d syl2anb rgen2 blbas unirnbl eqtri tgss2 mp3an mpbir blrn
      xp1st blopn xp2nd wal syl2anr simpr simplll simplrl elbl2 mpanl1 syl12anc
      simpllr simplrr bitr4d 3bitrd sseld anim12d pm4.71rd fveq2 oveq1d bibi12d
      eleq2d syl5ibr expdimp exlimivv sylbi opelxp 3bitr4g alrimivv relxp relss
      ee10 eqrel sylancl xpeq1 eqeq2d xpeq2 syl3anc ovex sylibr eleq1 rexlimivv
      rcla42ev ssriv tgss eqssi opnval 3eqtr4i ) UAUKZUBUKZUCUKZULZQZUCBURUBAUR
      ZUAUUAZUMUNZCUOUNZUUBZUMUNZABUUDRZCUUEUNZVVFVVIVVFVVIUPZUDUKZUEUKZSZVVMUF
      UKZSZVVPVVNUPZTZUFVVHURZUQZUEVVEUSUDVVEUTZUSZVWAUDUEVWBVVEVVMVWBSVVMFGULZ
      SZVVNVVBQZUCBURUBAURZVWAVVNVVESVWBVWDVVMVWDVVJUTZVVFUTZVWBABFGDVASZAVBSZN
      DAJVCVDZEVASZBVBSZOEBKVCVDZVWJFAUTQNDAFLJVEVDVWMGBUTQOEBGMKVEVDUUFZVVJVVF
      VWKVWNVVJVVFQVWLVWOUAUCUBABUUCVFZUUGZVVEVJSZVWIVWBQVWKVWNVWSVWLVWOUAUCUBA
      BUUHVFZVVEUUIVDZVGUUJVVDVWGUAVVNUEVKVUSVVNQVVCVWFUBUCABVUSVVNVVBVHVIVLVWE
      VWGVWAVWEVWFVWAUBUCABVWEVUTASZVVABSZTZVWFVWAUQVWEVXDTZVWAVWFVVMVVBSZVVQVV
      PVVBUPZTZUFVVHURZUQVXEVXFVXIVXEVXFTVXIVVMUGUKZVVGRZVVBUPZUGVMURZVXEVXFVXM
      VXFVVMUHUKZUIUKZVNZQZVXNVUTSZVXOVVASZTZTZUIVOUHVOVXEVXMUHUIVVMVUTVVAVPVXE
      VYAVXMUHUIVXDVYAVXMUQVWEVXDVXQVXTVXMVXDVXTVXQVXMVXDVXTVXQVXMUQVXDVXTTZVXM
      VXQVXPVXJVVGRZVVBUPZUGVMURZVYBVXNFSZVXOGSZVXNVVMDUOUNZRZVUTUPZUDVMURZVXOV
      VNEUOUNZRZVVAUPZUEVMURZVYEVXBVXRVYFVXCVXSVXBVUTFUPZVXRVYFVWJVXBVYPNVUTDAF
      LJVQVRVUTFVXNVSVTWAVXCVXSVYGVXBVXRVXCVVAGUPZVXSVYGVWMVXCVYQOVVAEBGMKVQVRV
      VAGVXOVSVTWBVXBVXRVYKVXCVXSVXBUJUKZVVMVYHRZVUTUPZUDVMURZUJVUTUSZVXRVYKVXB
      VYPWUBVWJVXBVYPWUBTWCNUJUDVUTDAFLJWDVDWEWUAVYKUJVXNVUTVYRVXNQZVYTVYJUDVMW
      UCVYSVYIVUTVYRVXNVVMVYHWFWGWHWIVTWAVXCVXSVYOVXBVXRVXCVUSVVNVYLRZVVAUPZUEV
      MURZUAVVAUSZVXSVYOVXCVYQWUGVWMVXCVYQWUGTWCOUAUEVVAEBGMKWDVDWEWUFVYOUAVXOV
      VAVUSVXOQZWUEVYNUEVMWUHWUDVYMVVAVUSVXOVVNVYLWFWGWHWIVTWBVYKVYOTVYFVYGTZVY
      JVYNTZUEVMURUDVMURZVYEVYJVYNUDUEVMVMUUKWUIWUKVYEWUIWUJVYEUDUEVMVMWUIVVMVM
      SZVVNVMSZTZWUJVYEWUIWUNWUJTTVVMVVNWJWKZVVMVVNWOZVMSZVXPWUPVVGRZVVBUPZVYEW
      UNWUQWUIWUJWUOVVMVVNVMUUOZUULWUIWUNWUJWUSWUIWUNTZVYJVXNWUPVYHRZVUTUPZVYNV
      XOWUPVYLRZVVAUPZWUSWVAWVBVYIUPZVYJWVCUQWVAVWJVYFTZWUQWULWUPVVMWJWKZWVFVYF
      WVGVYGWUNVYFVWJNWLWMWUNWUQWUIWUTWNZWUIWULWUMUUMWUNWVHWUIWULVVMXHSZVVNXHSZ
      WVHWUMVVMWPZVVNWPZVVMVVNUUPWQWNDVXNWUPVVMFLWRWSWVBVYIVUTWTXAWVAWVDVYMUPZV
      YNWVEUQWVAVWMVYGTZWUQWUMWUPVVNWJWKZWVNVYGWVOVYFWUNVYGVWMOWLUUNWVIWUIWULWU
      MUUQWUNWVPWUIWULWVJWVKWVPWUMWVLWVMVVMVVNUURWQWNEVXOWUPVVNGMWRWSWVDVYMVVAW
      TXAWVAWURWVBWVDULZUPWVCWVETWVQVVBUPWUSWVAUAWURWVQWUNWUIWUQVUSWURSZVUSWVQS
      ZUQWUTWUIWUQTZVUSVWDSZVXOVUSXBUNZERZVXNVUSXCUNZDRZXDWKZWWEWWCWOZWUPXDWKZT
      ZVUSWWDWWBVNQZWWDFSZWWEWUPXDWKZTZWWBGSZWWCWUPXDWKZTZTZTZWVRWVSWWIWWJWWKWW
      NTZWWHTZTZWVTWWRWWIWWJWWSTZWWHTWXAWWAWXBWWHVUSFGXEXFWWJWWSWWHUUSUUTWVTWWT
      WWQWWJWVTWWTWWSWWLWWOTZTZWWQWVTWWTWXDWVTWWSWWHWXCWUIWWSWUQWWHWXCWCZWUIWWS
      TWWEXHSZWWCXHSZTZWUPXHSZWXEWUQVYFWWKVYGWWNWXHVYFWWKTWXFVYGWWNTWXGVWJVYFWW
      KWXFNVXNWWDDFLXGXIVWMVYGWWNWXGOVXOWWBEGMXGXIXJXTWUPWPWXFWXGWXIWXEWXFWXGWX
      IUVAZWWHWWEWWCWJWKZWWCWWEWOZWUPXDWKWXCWXJWWGWXLWUPXDWXJWWGWXKYAZWWEWWCWOW
      XLWXJWWFWXMWWEWWCWXFWXGWWFWXMWCZWXIWXGWXFWXNWWCWWEXKUVBUVCXLWXKWWEWWCXMXN
      XOWWEWWCWUPXPXQXRWQXSYBUVDWWKWWNWWLWWOUVGUVEUVFYCWVTWVRWWAVXPVUSCRZWUPXDW
      KZTZWWIWUIVXPVWDSZWUQWVRWXQWCZVXNVXOFGYDZCVASZWXRWUQWXSHIDECFGLMNOPUVHZVU
      SCVXPWUPVWDCYLZYLVWDVWDULZYLVWDWYCWYDHIVWDVWDHUKZXCUNIUKZXCUNDRWYEXBUNWYF
      XBUNERUVIZXHXDUVJCPXHWYGXDUVKUVLUVMUVSVWDUVNUVOZYEXIVTWVTWWAWXPWWHWUIWWAW
      XPWWHWCZUQZWUQWUIWXRWYJWXTWXRWWAWYIWXRWWATWXOWWGWUPXDHIDECVXPVUSVXNVXOWWD
      WWBFGLMNOPVXPXCUNZVXNVXNVXOUHVKZYFZYGZVXPXBUNZVXOVXNVXOWYLUIVKYHZYGZWWDYI
      WWBYIYJXOYKXAYMUVPXQWVSWWJWWDWVBSZWWBWVDSZTZTWVTWWRVUSWVBWVDXEWVTWYTWWQWW
      JWVTWYRWWMWYSWWPVYFWUQWYRWWMWCZVYGVWJVYFWUQXUANWWDDVXNWUPFLYEXIYNVYGWUQWY
      SWWPWCZVYFVWMVYGWUQXUBOWWBEVXOWUPGMYEXIUVQYOYPUVRUVTUWAUWBWVBVUTWVDVVAUWC
      WURWVQVVBWTUWDUWEUWFVYDWUSUGWUPVMVXJWUPQVYCWURVVBVXJWUPVXPVVGUWGWGUWHUWJU
      WIUWKYQUWLUWMVXQVXLVYDUGVMVXQVXKVYCVVBVVMVXPVXJVVGWFWGWHYRYKUWNUWOWNUWTYC
      YQVWEVXIVXMWCZVXDVXFWYAVWEXUCWYBUFVVBCVVMVWDUGWYHUWPVRWMUWQYKVWFVVOVXFVVT
      VXIVVNVVBVVMUWRVWFVVSVXHUFVVHVWFVVRVXGVVQVVNVVBVVPUWSYPWHUXAYRYKUWKYQUXBU
      XCVWSVVHVJSZVWBVVHUTZQVVLVWCWCVWTWYAXUDWYBCUXDVDZXUEVWDVWIVWBWYAXUEVWDQWY
      BCVWDWYHUXEVDVWDVWHVWIVWPVWRUXFVXAVGUDUEUFVVEVVHUXGUXHUXIXUDVWSVVHVVEUPVV
      IVVFUPXUFVWTUFVVHVVEVVPVVHSZVVPVXKQZUGVMURUDVWDURZVVPVVESZWYAXUGXUIWCWYBU
      DVVPCVWDUGWYHUXJVDXUHXUJUDUGVWDVMVWEVXJVMSZTZXUJXUHVXKVVESZXULVXKVVBQZUCB
      URUBAURZXUMXULVVMXCUNZVXJVYHRZASZVVMXBUNZVXJVYLRZBSZVXKXUQXUTULZQZXUOVWEX
      UPFSZXUKXURVVMFGUXKVWJXVDXUKXURNDXUPVXJAFLJUXLXIVTVWEXUSGSZXUKXVAVVMFGUXM
      VWMXVEXUKXVAOEXUSVXJBGMKUXLXIVTXULXVCVUTVVAVNZVXKSZXVFXVBSZWCZUCUXNUBUXNZ
      XULXVIUBUCXULXVGXVFVWDSZVVMXVFCRZVXJXDWKZTZXVHWYAVWEXUKXVGXVNWCWYBXVFCVVM
      VXJVWDWYHYEXIXULVUTFSZVVAGSZTZXVMTZVUTXUQSZVVAXUTSZTZXVNXVHVWEXUKXVRXWAWC
      ZVWEVXQWUITZUIVOUHVOXUKXWBUQZUHUIVVMFGVPXWCXWDUHUIVXQWUIXUKXWBWUIXUKTZXWB
      VXQXVQVXPXVFCRZVXJXDWKZTZVUTVXNVXJVYHRZSZVVAVXOVXJVYLRZSZTZWCXWEXWHXVQXWM
      TXWMXWEXVQXWGXWMWUIXVQXUKXWGXWMWCWUIXVQTZXUKTZXWGVXOVVAERZVXNVUTDRZXDWKZX
      WQXWPWOZVXJXDWKXWQXWPWJWKZXWPXWQWOZVXJXDWKZXWMXWOXWFXWSVXJXDXWNXWFXWSQZXU
      KWUIWXRXVKXXCXVQWXTVUTVVAFGYDHIDECVXPXVFVXNVXOVUTVVAFGLMNOPWYNWYQXVFXCUNV
      UTVUTVVAUBVKZYFYGXVFXBUNVVAVUTVVAXXDUCVKZYHYGYJWQYMXOXWOXWSXXAVXJXDXWNXWS
      XXAQXUKXWNXWSXWTYAZXWQXWPWOXXAXWNXWRXXFXWQXWPVYFXVOVYGXVPXWRXXFWCZVYGXVPT
      ZXWPXHSZXWQXHSZXXGVYFXVOTZVWMVYGXVPXXIOVXOVVAEGMXGXIZVWJVYFXVOXXJNVXNVUTD
      FLXGXIZXWPXWQXKUXOXTXLXWTXWQXWPXMXNYMXOXWOXXBXWQVXJXDWKZXWPVXJXDWKZTZXWMX
      WNXXJXXITZVXJXHSZXXBXXPWCZXUKVYFXVOVYGXVPXXQXXKXXJXXHXXIXXMXXLXJXTVXJWPXX
      JXXIXXRXXSXWQXWPVXJXPXRWQXWOXWJXXNXWLXXOXWOXUKVYFXVOXWJXXNWCZXWNXUKUXPZVY
      FVYGXVQXUKUXQWUIXVOXVPXUKUXRVWJXUKXXKXXTNVUTDVXNVXJFLUXSUXTUYAXWOXUKVYGXV
      PXWLXXOWCZXYAVYFVYGXVQXUKUYBWUIXVOXVPXUKUYCVWMXUKXXHXYBOVVAEVXOVXJGMUXSUX
      TUYAYOUYDUYEXSYBXWEXWMXVQXWEXWJXVOXWLXVPXWEXWIFVUTVYFXUKXWIFUPZVYGVWJVYFX
      UKXYCNDVXNVXJFLYSXIYNUYFXWEXWKGVVAVYGXUKXWKGUPZVYFVWMVYGXUKXYDOEVXOVXJGMY
      SXIUVQUYFUYGUYHUYDVXQXVRXWHXWAXWMVXQXVMXWGXVQVXQXVLXWFVXJXDVVMVXPXVFCWFXO
      YPVXQXVSXWJXVTXWLVXQXUQXWIVUTVXQXUPVXNVXJVYHVXQXUPWYKVXNVVMVXPXCUYIWYMXNU
      YJUYLVXQXUTXWKVVAVXQXUSVXOVXJVYLVXQXUSWYOVXOVVMVXPXBUYIWYPXNUYJUYLYOUYKUY
      MUYNUYOUYPYQXVKXVQXVMVUTVVAFGXXEUYQXFVUTVVAXUQXUTXXEUYQUYRXQUYSXULVXKYTZX
      VBYTXVCXVJWCXULVXKVWDUPZVWDYTXYEWYAVWEXUKXYFWYBCVVMVXJVWDWYHYSXIFGUYTVXKV
      WDVUAVUBXUQXUTUYTUBUCVXKXVBVUCVUDUWQXUNXVCVXKXUQVVAULZQUBUCXUQXUTABVUTXUQ
      QVVBXYGVXKVUTXUQVVAVUEVUFVVAXUTQXYGXVBVXKVVAXUTXUQVUGVUFVUMVUHVVDXUOUAVXK
      VVMVXJVVGVUIVUSVXKQVVCXUNUBUCABVUSVXKVVBVHVIVLVUJVVPVXKVVEVUKYRVULUYPVUNV
      VHVVEVUOUXHVUPVWQWYAVVKVVIQWYBCVVKVVKYIVUQVDVUR $.
      $( [5-Jan-2014] $) $( [11-Jun-2010] $)
  $}

