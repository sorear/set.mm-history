$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The reals are uncountable
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F k n x y $.  $d T n y $.
    rpnnen1.1 $e |- T = { n e. ZZ | ( n / k ) < x } $.
    rpnnen1.2 $e |- F = ( x e. RR |->
        ( k e. NN |-> ( sup ( T , RR , < ) / k ) ) ) $.
    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem1 $p |- ( x e. RR -> ( F ` x ) e. ( QQ ^m NN ) ) $=
      ( vy cv cr wcel cn clt co cq cvv cz wa wbr wrex cfv csup cdiv cmpt mptexg
      cmap wceq nnex ax-mp fvmpt2 mpan2 wf wss crab ssrab2 eqsstri a1i wne wral
      c0 cle cmul ax-mulrcl ancoms sylan2 btwnz simpld syl cc0 wb adantl simpll
      nnre zre nngt0 jca ad2antlr ltdivmul syl111anc mpbird rabn0 sylibr neeq1i
      rexbidva rabeq2i wi syl11anc ltle sylan2b ralrimiva breq2 ralbidv rcla4ev
      sylbid impr suprzcl sseldd znq sylancom ex eqid fmptd qex elmap eqeltrd )
      AIZJKZXFEUAZCLBJMUBZCIZUCNZUDZOLUFNZXGXLPKZXHXLUGLPKXNUHCLXKPUEUIAJXLPEGU
      JUKXGLOXLULXLXMKXGCLXKOXLXGXJLKZXKOKZXGXOXIQKXPXGXORZBQXIBQUMZXQBDIZXJUCN
      XFMSZDQUNZQFXTDQUOUPUQZXQXRBUTURZXSHIZVASZDBUSZHJTZXIBKYBXQYAUTURZYCXQXTD
      QTZYHXQYIXSXJXFVBNZMSZDQTZXQYJJKZYLXOXGXJJKZYMXJVMZYNXGYMXJXFVCZVDVEZYMYL
      YJXSMSDQTDDYJVFVGVHXQXTYKDQXQXSQKZRZXSJKZXGYNVIXJMSZRZXTYKVJYRYTXQXSVNVKZ
      XGXOYRVLZXOUUBXGYRXOYNUUAYOXJVOVPVQXSXFXJVRVSZWDVTXTDQWAWBBYAUTFWCWBXQYMX
      SYJVASZDBUSZYGYQXQUUFDBXSBKXQYRXTRUUFXTDBQFWEXQYRXTUUFYSXTYKUUFUUEYSYTYMY
      KUUFWFUUCYSYNXGYMXOYNXGYRYOVQUUDYPWGXSYJWHWGWNWOWIWJYFUUGHYJJYDYJUGYEUUFD
      BYDYJXSVAWKWLWMWGHDBWPVSWQXIXJWRWSWTXLXAXBOLXLXCUHXDWBXE $.
      $( [12-May-2013] $)


    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem2 $p |- ( ( x e. RR /\ k e. NN ) -> sup ( T , RR , < ) e. ZZ ) $=
      ( vy cv cr wcel wa cz clt co wbr c0 cle wrex syl11anc cn csup cdiv ssrab2
      wss crab eqsstri a1i wne wral cmul nnre ax-mulrcl ancoms sylan2 btwnz syl
      simpld cc0 zre adantl simpll nngt0 jca ad2antlr ltdivmul syl111anc mpbird
      wb rexbidva sylibr neeq1i rabeq2i ltle sylbid impr sylan2b ralrimiva wceq
      rabn0 wi breq2 ralbidv rcla4ev suprzcl sseldd ) AIZJKZCIZUAKZLZBMBJNUBZBM
      UEZWKBDIZWIUCOWGNPZDMUFZMFWODMUDUGUHZWKWMBQUIZWNHIZRPZDBUJZHJSZWLBKWQWKWP
      QUIZWRWKWODMSZXCWKXDWNWIWGUKOZNPZDMSZWKXEJKZXGWJWHWIJKZXHWIULZXIWHXHWIWGU
      MZUNUOZXHXGXEWNNPDMSDDXEUPURUQWKWOXFDMWKWNMKZLZWNJKZWHXIUSWINPZLZWOXFVIXM
      XOWKWNUTVAZWHWJXMVBZWJXQWHXMWJXIXPXJWIVCVDVEWNWGWIVFVGZVJVHWODMVTVKBWPQFV
      LVKWKXHWNXERPZDBUJZXBXLWKYADBWNBKWKXMWOLYAWODBMFVMWKXMWOYAXNWOXFYAXTXNXOX
      HXFYAWAXRXNXIWHXHWJXIWHXMXJVEXSXKTWNXEVNTVOVPVQVRXAYBHXEJWSXEVSWTYADBWSXE
      WNRWBWCWDTHDBWEVGWF $.
      $( [12-May-2013] $)

    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem3 $p |- ( x e. RR -> A. n e. ran ( F ` x ) n <_ x ) $=
      ( cr wcel cle wbr wceq cn wrex cq co wa clt cvv cz cv cfv crn wf wfn cmap
      wb rpnnen1lem1 qex nnex elmap sylib ffn fvelrnb 3syl csup cdiv cmpt ax-mp
      mptexg fvmpt2 mpan2 fveq1d eqid sylan9eq eqeq1d cmul wss c0 wne wral crab
      oprex ssrab2 eqsstri zssre sstri a1i ax-mulrcl ancoms sylan2 btwnz simpld
      nnre syl cc0 zre adantl simpll nngt0 ad2antlr ltdivmul syl111anc rexbidva
      mpbird rabn0 sylibr neeq1i rabeq2i wi syl11anc ltle sylbid impr ralrimiva
      jca sylan2b suprleub syl22anc rpnnen1lem2 simpl breq1 syl5ibcom rexlimdva
      ledivmul ralrimiv ) AUAZHIZDUAZXQJKZDXQEUBZUCZXRXSYBIZCUAZYAUBZXSLZCMNZXT
      XRMOYAUDZYAMUEYCYGUGXRYAOMUFPIYHABCDEFGUHOMYAUIUJUKULMOYAUMCMXSYAUNUOXRYF
      XTCMXRYDMIZQZYFBHRUPZYDUQPZXSLZXTYJYEYLXSXRYIYEYDCMYLURZUBZYLXRYDYAYNXRYN
      SIZYAYNLMSIYPUJCMYLSUTUSAHYNSEGVAVBVCYIYLSIYOYLLYKYDUQVMCMYLSYNYNVDVAVBVE
      VFYJYLXQJKZYMXTYJYQYKYDXQVGPZJKZYJBHVHZBVIVJZYRHIZXSYRJKZDBVKYSYTYJBTHBXS
      YDUQPXQRKZDTVLZTFUUDDTVNVOVPVQVRYJUUEVIVJZUUAYJUUDDTNZUUFYJUUGXSYRRKZDTNZ
      YJUUBUUIYIXRYDHIZUUBYDWDZUUJXRUUBYDXQVSZVTWAZUUBUUIYRXSRKDTNDDYRWBWCWEYJU
      UDUUHDTYJXSTIZQZXSHIZXRUUJWFYDRKZQZUUDUUHUGUUNUUPYJXSWGWHZXRYIUUNWIZYIUUR
      XRUUNYIUUJUUQUUKYDWJXFZWKXSXQYDWLWMZWNWOUUDDTWPWQBUUEVIFWRWQUUMYJUUCDBXSB
      IYJUUNUUDQUUCUUDDBTFWSYJUUNUUDUUCUUOUUDUUHUUCUVBUUOUUPUUBUUHUUCWTUUSUUOUU
      JXRUUBYIUUJXRUUNUUKWKUUTUULXAXSYRXBXAXCXDXGXEDBYRXHXIYJYKHIZXRUURYQYSUGYJ
      YKTIUVCABCDEFGXJYKWGWEXRYIXKYIUURXRUVAWHYKXQYDXOWMWOYLXSXQJXLXMXCXNXCXP
      $.
      $( [12-May-2013] $)

    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem4 $p |- ( x e. RR -> sup ( ran ( F ` x ) , RR , < ) e. RR ) $=
      ( vy cv cr wcel c0 wne cle wbr wral cn cq sylib syl cfv crn wss wrex csup
      clt wf cmap co rpnnen1lem1 qex nnex elmap frn qssre syl6ss cdm ne0i ax-mp
      c1 1nn fdm neeq1d mpbiri dm0rn0 necon3bii rpnnen1lem3 weq ralbidv rcla4ev
      breq2 mpdan suprcl syl111anc ) AIZJKZVOEUAZUBZJUCZVRLMZDIZHIZNOZDVRPZHJUD
      ZVRJUFUEJKVPQRVQUGZVSVPVQRQUHUIKWFABCDEFGUJRQVQUKULUMSZWFVRRJQRVQUNUOUPTV
      PWFVTWGWFVQUQZLMZVTWFWIQLMZUTQKWJVAQUTURUSWFWHQLQRVQVBVCVDWHLVRLVQVEVFSTV
      PWAVONOZDVRPZWEABCDEFGVGWDWLHVOJHAVHWCWKDVRWBVOWANVKVIVJVLHDVRVMVN $.
      $( [12-May-2013] $)


    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem5 $p |- ( x e. RR -> sup ( ran ( F ` x ) , RR , < ) = x ) $=
      ( vy cr wcel clt cle wbr wa c0 cn co syl syl11anc cz cv cfv crn csup wceq
      wn wb rpnnen1lem4 eqlelt mpancom wss wne wral cq wf cmap rpnnen1lem1 nnex
      qex elmap sylib frn qssre syl6ss cdm c1 1nn ne0i fdm neeq1d mpbiri dm0rn0
      ax-mp necon3bii id rpnnen1lem3 suprleub syl22anc cmin cdiv wrex cc0 mpdan
      resubcl adantr posdif biimpa gt0ne0 rereccl arch ex w3a caddc rpnnen1lem2
      zre 3adant3 ltp1 wi jca nnre ltrec1 syl2an ad2antrr nnrecre adantl simpll
      nngt0 ltaddsub2 syl111anc weq breq2 ralbidv rcla4ev 3jca wfn ffn fnfvelrn
      sylan suprub sseldd leadd1 mpbid ax-addrcl simpl lelttr exp3a mpd adantlr
      sylbird sylbid peano2zdi breq1d a1i ad2antlr sylibr cvv fvmpt2 mpan2 syld
      cc opreq1 elrab2 biimpri crab ssrab2 eqsstri zssre sstri ax-mulrcl ancoms
      cmul sylan2 btwnz simpld ltdivmul rexbidva mpbird rabn0 rabeq2i ltle impr
      neeq1i ralrimiva syldan recnd ax-1cn nncn nnne0 divdir cmpt mptexg fveq1d
      sylan2b oprex eqid sylan9eq opreq1d eqtr4d lenlt 3imtr3d exp31 com4l 3imp
      com14 mt2d 3exp rexlimdv pm2.01d mpbir2and ) AUAZIJZUWJEUBZUCZIKUDZUWJUEZ
      UWNUWJLMZUWNUWJKMZUFZUWNIJZUWKUWOUWPUWRNUGABCDEFGUHZUWNUWJUIUJUWKUWMIUKZU
      WMOULZUWKDUAZUWJLMZDUWMUMZUWPUWKPUNUWLUOZUXAUWKUWLUNPUPQJUXFABCDEFGUQUNPU
      WLUSURUTVAZUXFUWMUNIPUNUWLVBVCVDRZUWKUXFUXBUXGUXFUWLVEZOULZUXBUXFUXJPOULZ
      VFPJUXKVGPVFVHVMUXFUXIPOPUNUWLVIVJVKUXIOUWMOUWLVLVNVARZUWKVOABCDEFGVPZDUW
      MUWJVQVRUWKUWQUWKUWQVFUWJUWNVSQZVTQZCUAZKMZCPWAZUWRUWKUWQUXRUWKUWQNZUXOIJ
      ZUXRUXSUXNIJZUXNWBULZUXTUWKUYAUWQUWKUWSUYAUWTUWJUWNWDWCWEZUXSUYAWBUXNKMZU
      YBUYCUWKUWQUYDUWSUWKUWQUYDUGUWTUWNUWJWFUJWGZUXNWHSUXNWISUXOCWJRWKUWKUXQUW
      RCPUWKUXPPJZUXQUWRUWKUYFUXQWLZUWQBIKUDZUYHVFWMQZKMZUYGUYHIJZUYJUWKUYFUYKU
      XQUWKUYFNZUYHTJUYKABCDEFGWNZUYHWORZWPUYHWQRUWKUYFUXQUWQUYJUFZWRUWQUYFUXQU
      WKUYOUWKUWQUYFUXQUYOUWKUWQUYFUXQUYOWRUXSUYFNZUXQUXPUWLUBZVFUXPVTQZWMQZUWJ
      KMZUYOUYPUXQUYRUXNKMZUYTUXSUYAUYDNUXPIJZWBUXPKMZNZUXQVUAUGUYFUXSUYAUYDUYC
      UYEWSUYFVUBVUCUXPWTZUXPXGWSZUXNUXPXAXBUYPVUAUWNUYRWMQZUWJKMZUYTUYPUWSUYRI
      JZUWKVUHVUAUGUWKUWSUWQUYFUWTXCUYFVUIUXSUXPXDZXEUWKUWQUYFXFUWNUYRUWJXHXIUW
      KUYFVUHUYTWRZUWQUYLUYSVUGLMZVUKUYLUYQUWNLMZVULUYLUXAUXBUXCHUAZLMZDUWMUMZH
      IWAZWLZUYQUWMJZVUMUWKVURUYFUWKUXAUXBVUQUXHUXLUWKUXEVUQUXMVUPUXEHUWJIHAXJV
      UOUXDDUWMVUNUWJUXCLXKXLXMWCXNWEUWKUWLPXOZUYFVUSUWKUXFVUTUXGPUNUWLXPRPUXPU
      WLXQXRZHDUWMUYQXSSUYLUYQIJZUWSVUIVUMVULUGUYLUWMIUYQUWKUXAUYFUXHWEVVAXTZUW
      KUWSUYFUWTWEUYFVUIUWKVUJXEZUYQUWNUYRYAXIYBUYLUYSIJZVUGIJZUWKVULVUKWRUYLVV
      BVUIVVEVVCVVDUYQUYRYCSUWKUWSVUIVVFUYFUWTVUJUWNUYRYCXBUWKUYFYDVVEVVFUWKWLV
      ULVUHUYTUYSVUGUWJYEYFXIYGYHYIYJUWKUYFUYTUYOWRUWQUYLUYIUXPVTQZUWJKMZUYIUYH
      LMZUYTUYOUYLVVHVVIUYLVVHUYIBJZVVIUYLUYITJZVVHVVJUYLUYHUYMYKZVVJVVKVVHNUXC
      UXPVTQZUWJKMZVVHDUYITBUXCUYIUEVVMVVGUWJKUXCUYIUXPVTUUAYLFUUBUUCXRUYLBIUKZ
      BOULZVUODBUMZHIWAZWLVVJVVIUYLVVOVVPVVRVVOUYLBTIBVVNDTUUDZTFVVNDTUUEUUFUUG
      UUHYMUYLVVSOULZVVPUYLVVNDTWAZVVTUYLVWAUXCUXPUWJUUKQZKMZDTWAZUYLVWBIJZVWDU
      YFUWKVUBVWEVUEVUBUWKVWEUXPUWJUUIZUUJUULZVWEVWDVWBUXCKMDTWADDVWBUUMUUNRUYL
      VVNVWCDTUYLUXCTJZNZUXCIJZUWKVUDVVNVWCUGVWHVWJUYLUXCWOXEZUWKUYFVWHXFZUYFVU
      DUWKVWHVUFYNUXCUWJUXPUUOXIZUUPUUQVVNDTUURYOBVVSOFUVBYOUYLVWEUXCVWBLMZDBUM
      ZVVRVWGUYLVWNDBUXCBJUYLVWHVVNNVWNVVNDBTFUUSUYLVWHVVNVWNVWIVVNVWCVWNVWMVWI
      VWJVWEVWCVWNWRVWKVWIVUBUWKVWEUYFVUBUWKVWHVUEYNVWLVWFSUXCVWBUUTSYJUVAUVMUV
      CVVQVWOHVWBIVUNVWBUEVUOVWNDBVUNVWBUXCLXKXLXMSXNHDBUYIXSXRUVDWKUYLVVGUYSUW
      JKUYLVVGUYHUXPVTQZUYRWMQZUYSUYLUYHYTJVFYTJZUXPYTJZUXPWBULZNZVVGVWQUEUYLUY
      HUYNUVEVWRUYLUVFYMUYFVXAUWKUYFVWSVWTUXPUVGUXPUVHWSXEUYHVFUXPUVIXIUYLUYQVW
      PUYRWMUWKUYFUYQUXPCPVWPUVJZUBZVWPUWKUXPUWLVXBUWKVXBYPJZUWLVXBUEPYPJVXDURC
      PVWPYPUVKVMAIVXBYPEGYQYRUVLUYFVWPYPJVXCVWPUEUYHUXPVTUVNCPVWPYPVXBVXBUVOYQ
      YRUVPUVQUVRYLUYLUYIIJZUYKVVIUYOUGUYLVVKVXEVVLUYIWORUYNUYIUYHUVSSUVTYHYSUW
      AUWBUWDUWCUWEUWFUWGYSUWHUWI $.
      $( [12-May-2013] $)

    $( One half of ~ rpnnen , where we show an injection from the real numbers
       to sequences of rational numbers.  Specifically, we map a real number
       ` x ` to the sequence ` ( F `` x ) : NN --> QQ ` such that
       ` ( ( F `` x ) `` k ) ` is the largest rational number with denominator
       ` k ` that is strictly less than ` x ` .  In this manner, we get a
       monotonically increasing sequence that converges to ` x ` , and since
       each sequence converges to a unique real number, this mapping from reals
       to sequences of rational numbers is injective.  (Contributed by Mario
       Carneiro, 13-May-2013.) $)
    rpnnen1 $p |- RR ~<_ ( QQ ^m NN ) $=
      ( vy cr cvv wcel cq cn cv cfv wceq crn clt csup supeq1d cmap co cdom reex
      wbr rpnnen1lem1 wa rneq rpnnen1lem5 fveq2 rneqd eqeq12d vtoclga eqeqan12d
      weq id syl5ib impbid1 dom2 ax-mp ) IJKILMUAUBZUCUEUDAHIVAANZEOZHNZEOZJABC
      DEFGUFVBIKZVDIKZUGZVCVEPZAHUOZVIVCQZIRSZVEQZIRSZPVHVJVIIVKVMRVCVEUHTVFVGV
      LVBVNVDABCDEFGUIZVLVBPVNVDPAVDIVJVLVNVBVDVJIVKVMRVJVCVEVBVDEUJZUKTVJUPULV
      OUMUNUQVPURUSUT $.
      $( [12-May-2013] $)
  $}

  ${
    $d A k x z $.  $d B k x z $.  $d F k x z $.  $d M j k x z $.  $d k y $.
    rpnnen2.1 $e |- F = ( x e. ~P NN |->
      ( k e. NN |-> if ( k e. x , ( ( 1 / 3 ) ^ k ) , 0 ) ) ) $.
    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem1 $p |- ( ( A C_ NN /\ k e. NN ) ->
        ( ( F ` A ) ` k ) = if ( k e. A , ( ( 1 / 3 ) ^ k ) , 0 ) ) $=
      ( cn wss cv wcel cfv c1 c3 co cexp cc0 cif cmpt wceq nnex cvv elpw2 eleq2
      cdiv cpw ifbid adantr mpteq2dva mptexg ax-mp fvmpt sylbir fveq1d oprex cr
      wel 0re elisseti ifex eqid fvmpt2 mpan2 sylan9eq ) BFGZCHZFIZVDBDJZJVDCFV
      DBIZKLUCMZVDNMZOPZQZJZVJVCVDVFVKVCBFUDZIVFVKRBFSUAABCFCAUOZVIOPZQVKVMDAHZ
      BRZCFVOVJVQVOVJRVEVQVNVGVIOVPBVDUBUEUFUGEFTIVKTISCFVJTUHUIUJUKULVEVJTIVLV
      JRVGVIOVHVDNUMOUNUPUQURCFVJTVKVKUSUTVAVB $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem2 $p |- ( A C_ NN -> ( F ` A ) : NN --> RR ) $=
      ( cn wss cr wf cv wcel c1 c3 co cc0 cif cmpt wceq nnex cvv cfv wi cn0 1re
      cdiv cexp 3nn nndivre mp2an nnnn0 reexpcl sylancr ifcl sylancl eqid fmptd
      0re a1i cpw elpw2 eleq2 ifbid adantr mpteq2dva mptexg ax-mp sylbir mpbird
      wel fvmpt feq1d ) BFGZFHBDUAZIFHCFCJZBKZLMUENZVNUFNZOPZQZIVLCFVRHVSVNFKZV
      RHKZUBVLVTVQHKZOHKWAVTVPHKZVNUCKWBLHKMFKWCUDUGLMUHUIVNUJVPVNUKULUQVOVQOHU
      MUNURVSUOUPVLFHVMVSVLBFUSZKVMVSRBFSUTABCFCAVIZVQOPZQVSWDDAJZBRZCFWFVRWHWF
      VRRVTWHWEVOVQOWGBVNVAVBVCVDEFTKVSTKSCFVRTVEVFVJVGVKVH $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem3 $p |- ( + seq1 ( F ` NN ) ) ~~>
        ( ( 1 / 3 ) / ( 1 - ( 1 / 3 ) ) ) $=
      ( vy c1 c3 co cn cfv cv cmpt wcel wceq cc0 cvv cr mp2an clt wbr cdiv cexp
      wa copab cpw nnex pwid eleq2 ifbid iftrue sylan9eq mpteq2dva mptexg ax-mp
      wel cif fvmpt df-mpt eqtri 1re 3nn nndivre cabs cle 0re 3re 3pos recgt0ii
      recni ltleii absid 1lt3 wb recgt1 mpbi eqbrtri geolim1i ) EFGUAHZBICJZVSB
      IVRBKZUBHZLZVTIMZEKWANUCBEUDIIUEZMVSWBNIUFUGAIBIBAUOZWAOUPZLWBWDCAKZINZBI
      WFWAWHWCWFWCWAOUPWAWHWEWCWAOWGIVTUHUIWCWAOUJUKULDIPMWBPMUFBIWAPUMUNUQUNBE
      IWAURUSVRFQMGIMVRQMZUTVAFGVBRZVIVRVCJZVRFSWIOVRVDTWKVRNWJOVRVEWJGVFVGVHVJ
      VRVKRFGSTZVRFSTZVLGQMOGSTWLWMVMVFVGGVNRVOVPVQ $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem4 $p |- ( ( A C_ B /\ B C_ NN /\ k e. NN ) ->
      ( 0 <_ ( ( F ` A ) ` k ) /\ ( ( F ` A ) ` k ) <_ ( ( F ` B ) ` k ) ) ) $=
      ( wss cn wcel cc0 cfv cle wbr wa c1 c3 cr syl wceq sylan9eq w3a cdiv cexp
      cv co simpl3 cn0 nnnn0 0re 1re 3nn nndivre mp2an 3re 3pos recgt0ii ltleii
      expge0 mp3an1 sylancl cif rpnnen2lem1 sylan 3impa iftrue breqtrrd reexpcl
      sstr sylancr leid 3syl 3adant1 adantr ssel2 3ad2antl1 eqtrd 3brtr4d leidi
      jca wn iffalse syl5breqr pm2.61dan eqbrtrd ) BCGZCHGZDUDZHIZUAZWGBIZJWGBE
      KKZLMZWKWGCEKKZLMZNWIWJNZWLWNWOJOPUBUEZWGUCUEZWKLWOWHJWQLMZWEWFWHWJUFZWHW
      GUGIZJWPLMZWRWGUHZJWPUIOQIPHIWPQIZUJUKOPULUMZPUNUOUPUQXCWTXAWRXDWPWGURUSU
      TZRWIWJWKWJWQJVAZWQWEWFWHWKXFSZWEWFNBHGWHXGBCHVHABDEFVBVCVDZWJWQJVETZVFWO
      WQWQWKWMLWOWHWQQIZWQWQLMWSWHXCWTXJXDXBWPWGVGVIWQVJVKXIWOWMWGCIZWQJVAZWQWI
      WMXLSZWJWFWHXMWEACDEFVBVLZVMWEWFWJXLWQSZWHWEWJNXKXOBCWGVNXKWQJVEZRVOVPVQV
      SWIWJVTZNZWLWNXRJJWKLJUIVRZWIXQWKXFJXHWJWQJWATZWBXRWKJWMLXTWIJWMLMZXQWIXK
      YAWIXKNZJWQWMLYBWHWRWEWFWHXKUFXERWIXKWMXLWQXNXPTVFWIXKVTZNJJWMLXSWIYCWMXL
      JXNXKWQJWATWBWCVMWDVSWC $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem5 $p |- ( ( A C_ NN /\ M e. NN ) ->
        E. z ( <. M , + >. seqz ( F ` A ) ) ~~> z ) $=
      ( cn wss wcel caddc cfv cseqz co cli wbr c1 cr cmin cc cop cv wex wi nnex
      cpw elpw2 c0 cif wceq fveq2 opreq2d breq1d exbidv imbi2d cuz wo cseq1 crn
      uzp1 clt csup opeq1 opreq1d addex fvex seq1seqz c3 cdiv oprex rpnnen2lem2
      wf ssid ax-mp 0elpw elimel elpwi mp2b cc0 rpnnen2lem4 mp3an12 rpnnen2lem3
      cle wa cvgcmpi eqbrtrri syl6eqbr ltso supex breq2 cla4ev syl 1z ax-resscn
      eluzsubi fss mp2an nnuz feq2i mpbi clim2serzi cz eluzelz zcn ax-1cn npcan
      3syl sylancl opeq1d mpbid jaoi eleq2s dedth sylbir imp ) CHIZFHJZFKUAZCEL
      ZMNZBUBZOPZBUCZXPCHUFZJZXQYCUDZCHUEUGYEYFXQXRYECUHUIZELZMNZYAOPZBUCZUDCUH
      CYGUJZYCYKXQYLYBYJBYLXTYIYAOYLXSYHXRMCYGEUKULUMUNUOYKFQUPLZHFYMJFQUJZFQQK
      NZUPLJZUQYKQFUTYNYKYPYNYIKYHURNZUSZRVAVBZOPZYKYNYIQKUAZYHMNZYSOYNXRUUAYHM
      FQKVCVDYQUUBYSOKYHVEYGEVFZVGDQVHVINZQUUDSNZVINHELZYHUUDUUEVIVJHHIZHRUUFVL
      HVMZAHDEGVKVNYGYDJZYGHIZHRYHVLZCUHYDHVOVPZYGHVQZAYGDEGVKVRZUUJUUGDUBZHJVS
      UUOYHLZWCPUUPUUOUUFLWCPWDUUIUUJUULUUMVNUUHAYGHDEGVTWAADEGWBWEWFZWGYJYTBYS
      RYRVAWHWIZYAYSYIOWJWKWLYPFQSNZQKNZKUAZYHMNZYAOPZBUCZYKYPUUSYMJUVBYSUUSUUB
      LZSNZOPZUVDQQFWMWMWOYSYHQUUSUUCUURUUQHTYHVLZYMTYHVLUUKRTIUVHUUNWNHRTYHWPW
      QHYMTYHWRWSWTXAUVCUVGBUVFYSUVESVJYAUVFUVBOWJWKXGYPUVCYJBYPUVBYIYAOYPUVAXR
      YHMYPUUTFKYPFTJZQTJUUTFUJYPFXBJUVIYOFXCFXDWLXEFQXFXHXIVDUMUNXJXKWLWRXLXMX
      NXO $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem6 $p |- ( ( A C_ NN /\ M e. NN ) ->
        sum_ k e. ( ZZ>= ` M ) ( ( F ` A ) ` k ) e. RR ) $=
      ( vz cn wss wcel wa cz cv cfv cr cuz wral syl c1 nnuz caddc cop cseqz cli
      co wbr wex csu nnz adantl wf rpnnen2lem2 wfn ffnfv simprbi wi uzss eleq2s
      syl6sseqr ssralv mpan9 rpnnen2lem5 fvex isumrecl syl111anc ) BHIZEHJZKELJ
      ZCMBDNZNZOJZCEPNZQZEUAUBVIUCUEGMUDUFGUGVLVJCUHOJVGVHVFEUIUJVFVKCHQZVGVMVF
      HOVIUKZVNABCDFULVOVIHUMVNCHOVIUNUORVGVLHIVNVMUPVGVLSPNZHVLVPIEVPHSEUQTURT
      USVKCVLHUTRVAAGBCDEFVBGCVIEBDVCVDVE $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem7 $p |- ( ( A C_ B /\ B C_ NN /\ M e. NN ) ->
        sum_ k e. ( ZZ>= ` M ) ( ( F ` A ) ` k ) <_
        sum_ k e. ( ZZ>= ` M ) ( ( F ` B ) ` k ) ) $=
      ( vz wss cn wcel w3a cfv cli wbr cr cle wral wa simprd caddc cop cseqz co
      cuz cv csu cz wex nnz 3ad2ant3 sstr rpnnen2lem5 sylan 3impa fvex syl11anc
      isumclim2 3adant1 c1 uzss nnuz eleq2s syl6sseqr 3adant3 rpnnen2lem2 ffnfv
      wfn wf sylib syl 3ad2ant2 cc0 rpnnen2lem4 3expa ralrimiva r19.26-3 sylibr
      3jca ssralv sylc sumex iserzcmp syl22anc ) BCIZCJIZFJKZLZFUAUBZBEMZUCUDZF
      UEMZDUFZWJMZDUGZNOZWICEMZUCUDZWLWMWQMZDUGZNOZFUHKZWNPKZWSPKZWNWSQOZLZDWLR
      ZWOWTQOWHXBWKHUFZNOHUIZWPWGWEXBWFFUJUKZWEWFWGXIWEWFSZBJIZWGXIBCJULZAHBDEF
      GUMUNUOHDWJFBEUPZURUQWHXBWRXHNOHUIZXAXJWFWGXOWEAHCDEFGUMUSHDWQFCEUPZURUQX
      JWHWLJIZXFDJRZXGWGWEXQWFWGWLUTUEMZJWLXSIFXSJUTFVAVBVCVBVDUKWHXCDJRZXDDJRZ
      XEDJRZLXRWHXTYAYBWHXLXTWEWFXLWGXMVEXLWJJVHZXTXLJPWJVIYCXTSABDEGVFDJPWJVGV
      JTVKWFWEYAWGWFWQJVHZYAWFJPWQVIYDYASACDEGVFDJPWQVGVJTVLWEWFYBWGXKXEDJWEWFW
      MJKZXEWEWFYELVMWNQOXEABCDEGVNTVOVPVEVSXCXDXEDJVQVRXFDWLJVTWAWOWTDWJWQFWLW
      NDWBWLWSDWBXNXPWCWD $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem8 $p |- ( ( A C_ NN /\ M e. ( ZZ>= ` ( 1 + 1 ) ) ) ->
       sum_ k e. NN ( ( F ` A ) ` k ) =
         ( sum_ k e. ( 1 ... ( M - 1 ) ) ( ( F ` A ) ` k ) +
           sum_ k e. ( ZZ>= ` M ) ( ( F ` A ) ` k ) ) ) $=
      ( vz cn wss c1 caddc co cuz cfv wcel csu cfz wceq cr cc cmin nnuz sumeq1i
      wa cv a1i cpw nnex elpw2 wi 1z eluzsubi cif opreq2 opreq1 fveq2d opreq12d
      sumeq1d eqeq2d imbi2d c0 fveq2 fveq1d sumeq2sdv eqeq12d eleqtri elimel wf
      1nn 0elpw elpwi rpnnen2lem2 mp2b feq2i mpbi ax-resscn fss mp2an cop cseqz
      cli wbr wex ax-mp rpnnen2lem5 isumspliti dedth syl syl5bir impcom eluzelz
      cz zcn ax-1cn npcan sylancl adantl opreq2d 3eqtrd ) BHIZEJJKLZMNOZUDZHCUE
      ZBDNZNZCPZJMNZXFCPZJEJUALZQLZXFCPZXJJKLZMNZXFCPZKLZXLEMNZXFCPZKLXGXIRXCHX
      HXFCUBUCUFXBWTXIXPRZWTBHUGZOZXBXSBHUHUIXBXJXHOZYAXSUJZJJEUKUKULYBYCYAXIJY
      BXJJUMZQLZXFCPZYDJKLZMNZXFCPZKLZRZUJXJJXJYDRZXSYKYAYLXPYJXIYLXLYFXOYIKYLX
      KYEXFCXJYDJQUNURYLXNYHXFCYLXMYGMXJYDJKUOUPURUQUSUTYAYKXHXDYABVAUMZDNZNZCP
      ZYEYOCPZYHYOCPZKLZRBVABYMRZXIYPYJYSYTXHXFYOCYTXDXEYNBYMDVBVCZVDYTYFYQYIYR
      KYTYEXFYOCUUAVDYTYHXFYOCUUAVDUQVEGCYNJYDUKXJJXHJHXHVIUBVFVGXHSYNVHZSTIXHT
      YNVHHSYNVHZUUBYMXTOZYMHIZUUCBVAXTHVJVGZYMHVKZAYMCDFVLVMHXHSYNUBVNVOVPXHST
      YNVQVRUUEJHOJKVSYNVTLGUEWAWBGWCUUDUUEUUFUUGWDVIAGYMCDJFWEVRWFWGWGWHWIWJXC
      XOXRXLKXCXNXQXFCXCXMEMXBXMERZWTXBETOZJTOUUHXBEWLOUUIXAEWKEWMWHWNEJWOWPWQU
      PURWRWS $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem9 $p |- ( M e. NN ->
       sum_ k e. ( ZZ>= ` M ) ( ( F ` ( NN \ { M } ) ) ` k ) =
         ( 0 + ( ( ( ( 1 / 3 ) ^ M ) x. ( 1 / 3 ) ) /
                 ( 1 - ( 1 / 3 ) ) ) ) ) $=
      ( vj cn wcel cfv csu c1 caddc co cc0 c3 cexp cc wbr wceq cr vz cuz cv csn
      cdif cdiv cmul cmin cz wral cop cseqz cli wex nnz wss uzss nnuz syl6sseqr
      eleq2s difss rpnnen2lem2 ax-mp ax-resscn fss mp2an wfn ffnfv simprbi ee10
      wf ssralv peano2nn rpnnen2lem5 sylancr fvex isum1p syl111anc eleq1 opreq2
      cif eqidd ifbieq12d cpw cmpt elpw2 mpbir wel eleq2 ifbid adantr mpteq2dva
      nnex weq cbvmptv syl6eq cvv mptexg fvmpt oprex 0re elisseti ifex wn snidg
      eldifn nsyl3 iffalse eqtrd opreq1 fveq2d sumeq1d sumeq2sdv eqeq12d elimel
      syl 1nn 1z nncni ax-1cn addcomi isumshft2i dedth a1i nnaddcl wne clt nnre
      wa crp nnrp ltaddrp syl2an ltne elsnc necon3bbii cn0 nnnn0 3re 3pos eldif
      sylibr sylanbrc iftrue 1re nndivre recni expadd 3eqtrrd sumeq12dv reexpcl
      3nn mp3an1 recnd cabs recgt0ii ltleii absid 1lt3 wb recgt1 mpbi geoisum1c
      cle eqbrtri mp3an3 sylancl 3eqtr2d opreq12d ) DGHZDUBIZBUCZGDUDZUEZCIZIZB
      JZDUVOIZDKLMZUBIZUVPBJZLMZNKOUFMZDPMZUWCUGMKUWCUHMUFMZLMUVJDUIHUVPQHZBUVK
      UJZUVSLUKUVOULMUAUCUMRUAUNZUVQUWBSDUOUVJUVKGUPUWFBGUJZUWGUVJUVKKUBIZGUVKU
      WJUPDUWJGKDUQURUTURUSGQUVOVKZUWIGTUVOVKZTQUPUWKUVNGUPZUWLGUVMVAZAUVNBCEVB
      VCVDGTQUVOVEVFUWKUVOGVGUWIBGQUVOVHVIVCUWFBUVKGVLVJUVJUWMUVSGHUWHUWNDVMAUA
      UVNBCUVSEVNVOUABUVODUVNCVPZVQVRUVJUVRNUWAUWELUVJUVRDUVNHZUWDNWAZNFDFUCZUV
      NHZUWCUWRPMZNWAZUWQGUVOUWRDSZUWSUWPUWTNUWDNUWRDUVNVSUWRDUWCPVTUXBNWBWCUVN
      GWDZHZUVOFGUXAWEZSUXDUWMUWNUVNGWMWFWGAUVNBGBAWHZUWCUVLPMZNWAZWEZUXEUXCCAU
      CZUVNSZUXIBGUVLUVNHZUXGNWAZWEUXEUXKBGUXHUXMUXKUXHUXMSUVLGHZUXKUXFUXLUXGNU
      XJUVNUVLWIWJWKWLBFGUXMUXABFWNZUXLUWSUXGNUWTNUVLUWRUVNVSUVLUWRUWCPVTUXONWB
      WCWOWPEGWQHUXEWQHWMFGUXAWQWRVCWSVCZUWPUWDNUWCDPWTNTXAXBZXCWSUVJUWPXDUWQNS
      UWPDUVMHUVJDGUVMXFDGXEXGUWPUWDNXHXPXIUVJUWAUWJDUVLLMZUVOIZBJZGUWDUXGUGMZB
      JZUWEUVJUWAUXTSUVJDKWAZKLMZUBIZUVPBJZUWJUYCUVLLMZUVOIZBJZSDKDUYCSZUWAUYFU
      XTUYIUYJUVTUYEUVPBUYJUVSUYDUBDUYCKLXJXKXLUYJUWJUXSUYHBUYJUXRUYGUVODUYCUVL
      LXJXKXMXNBUVOUYCKUYDUWOUYCGHUYCUIHDKGXQXOZUYCUOVCXRUYCKUYCUYKXSXTYAYBYCUV
      JGUWJUYAUXSBGUWJSUVJURYDUVJUXNYIZUXSUXRUVNHZUWCUXRPMZNWAZUYNUYAUYLUXRGHZU
      XSUYOSDUVLYEZFUXRUXAUYOGUVOUWRUXRSZUWSUYMUWTNUYNNUWRUXRUVNVSUWRUXRUWCPVTU
      YRNWBWCUXPUYMUYNNUWCUXRPWTUXQXCWSXPUYLUYMUYOUYNSUYLUYPUXRUVMHZXDZUYMUYQUY
      LUXRDYFZUYTUYLDTHZUXRTHZDUXRYGRZVUAUVJVUBUXNDYHZWKUYLUYPVUCUYQUXRYHXPUVJV
      UBUVLYJHVUDUXNVUEUVLYKDUVLYLYMDUXRYNVRUYSUXRDUXRDDUVLLWTYOYPUUBUXRGUVMUUA
      UUCUYMUYNNUUDXPUVJDYQHZUVLYQHZUYNUYASZUXNDYRZUVLYRUWCQHZVUFVUGVUHUWCKTHOG
      HUWCTHZUUEUULKOUUFVFZUUGZUWCDUVLUUHUUMYMUUIUUJUVJUWDQHZVUJUYBUWESZUVJUWDU
      VJVUKVUFUWDTHVULVUIUWCDUUKVOUUNVUMVUNVUJUWCUUOIZKYGRVUOVUPUWCKYGVUKNUWCUV
      DRVUPUWCSVULNUWCXAVULOYSYTUUPUUQUWCUURVFKOYGRZUWCKYGRZUUSOTHNOYGRVUQVURUU
      TYSYTOUVAVFUVBUVEUWDUWCBUVCUVFUVGUVHUVIXI $.
      $( [13-May-2013] $)

    ${
      $d A k m n $.  $d A k m x $.  $d B k m n $.  $d B k m x $.
      $d F k m x y $.  $d k ph $.  $d k m z $.
      rpnnen2.2 $e |- ( ph <-> ( ( A C_ NN /\ B C_ NN ) /\
        ( m e. ( A \ B ) /\
          A. n e. NN ( n < m -> ( n e. A <-> n e. B ) ) ) ) ) $.
      $( Lemma for ~ rpnnen2 . $)
      rpnnen2lem10 $p |- ( ph ->
          ( sum_ k e. NN ( ( F ` A ) ` k ) =
            sum_ k e. NN ( ( F ` B ) ` k ) ->
            sum_ k e. ( ZZ>= ` m ) ( ( F ` A ) ` k ) =
            sum_ k e. ( ZZ>= ` m ) ( ( F ` B ) ` k ) ) ) $=
        ( c1 wceq caddc co cfv wcel cn csu wa cr cv cuz wo wi wss cdif clt wral
        wbr wb eldifi ssel2 sylan2 ad2ant2r sylbi uzp1 nnuz eleq2s syl syl6reqr
        fveq2 sumeq1d eqeq12d biimpd a1i simpll rpnnen2lem8 sylan eqeq1d biimpa
        cmin cfz c3 cdiv cexp cc0 cif cle w3a nnz elfzm11 sylancr simp3d simprr
        cz 1z elfznn weq breq1 eleq1 bibi12d imbi12d rcla4cva ifbid rpnnen2lem1
        syl2an mpd simplr 3eqtr4d adantlr sumeq2dv opreq1d rpnnen2lem6 syl11anc
        adantr 3eqtr3d eluzsubi adantl fzssuz sseqtr4i wf rpnnen2lem2 wfn ffnfv
        simprbi 3syl ssralv mpsyl fsumrecl readdcan syl111anc syldan exp31 jaod
        ) AFUAZKLZYEKKMNUBOPZUCZQEUAZCHOOZERZQYIDHOZOZERZLZYEUBOZYJERZYPYMERZLZ
        UDZAYEQPZYHACQUEZDQUEZSZYECDUFPZGUAZYEUGUIZUUFCPZUUFDPZUJZUDZGQUHZSZSZU
        UAJUUBUUEUUAUUCUULUUEUUBYECPUUAYECDUKCQYEULUMUNUOZYHYEKUBOZQKYEUPUQURUS
        AYFYTYGYFYTUDAYFYOYSYFYKYQYNYRYFQYPYJEYFYPUUPQYEKUBVAUQUTZVBYFQYPYMEUUQ
        VBVCVDVEAYGYOYSAYGSZYOKYEKVKNZVLNZYMERZYQMNZUVAYRMNZLZYSUURYOSUUTYJERZY
        QMNZYNUVBUVCUURYOUVFYNLUURYKUVFYNAUUBYGYKUVFLAUUNUUBJUUBUUCUUMVFUOZBCEH
        YEIVGVHVIVJUURUVFUVBLYOUURUVEUVAYQMUURUUTYJYMEAYIUUTPZYJYMLYGAUVHSZYICP
        ZKVMVNNYIVONZVPVQZYIDPZUVKVPVQZYJYMUVIUVJUVMUVKVPUVIYIYEUGUIZUVJUVMUJZU
        VIYIWEPZKYIVRUIZUVOAUUAUVHUVQUVRUVOVSZUUOUUAUVHUVSUUAKWEPYEWEPUVHUVSUJW
        FYEVTYIKYEWAWBVJVHWCAUULYIQPZUVOUVPUDZUVHAUUNUULJUUDUUEUULWDUOYIUUSWGZU
        UKUWAGYIQGEWHZUUGUVOUUJUVPUUFYIYEUGWIUWCUUHUVJUUIUVMUUFYICWJUUFYIDWJWKW
        LWMWPWQWNAUUBUVTYJUVLLUVHUVGUWBBCEHIWOWPAUUCUVTYMUVNLUVHAUUNUUCJUUBUUCU
        UMWRUOZUWBBDEHIWOWPWSWTXAXBXEUURYNUVCLZYOAUUCYGUWEUWDBDEHYEIVGVHXEXFUUR
        UVDYSUURYQTPZYRTPZUVATPZUVDYSUJAUWFYGAUUBUUAUWFUVGUUOBCEHYEIXCXDXEAUWGY
        GAUUCUUAUWGUWDUUOBDEHYEIXCXDXEUURUUSUUPPZYMTPZEUUTUHZUWHYGUWIAKKYEWFWFX
        GXHUUTQUEUURUWJEQUHZUWKUUTUUPQKUUSXIUQXJAUWLYGAUUCQTYLXKZUWLUWDBDEHIXLU
        WMYLQXMUWLEQTYLXNXOXPXEUWJEUUTQXQXRYMEKUUSXSXDYQYRUVAXTYAVJYBYCYDWQ $.
        $( [13-May-2013] $)

      $( Lemma for ~ rpnnen2 . $)
      rpnnen2lem11 $p |- ( ( A C_ NN /\ B C_ NN ) ->
        ( E. m e. ( A \ B ) A. n e. NN ( n < m -> ( n e. A <-> n e. B ) ) ->
        sum_ k e. NN ( ( F ` A ) ` k ) =/= sum_ k e. NN ( ( F ` B ) ` k ) ) )
          $=
        ( cn clt wbr wcel cr c1 c3 co cc0 wceq vy vz wss wa cv wral cfv csu wne
        wb wi cdif simplr sylbi simpll simprl eldifi ssel2 syl11anc rpnnen2lem6
        cuz cdiv cexp caddc cn0 3nn nnrecre ax-mp nnnn0 syl reexpcl sylancr 0re
        ax-addrcl sylancl cmul cmin crp ax-mulrcl 1re resubcli 3re 1lt3 recgt1i
        sylan2 mp2an simpri posdifi mpbi elrpii rerpdivcl csn snssd ssdif sstrd
        cle ssconb mpbird a1i rpnnen2lem7 syl111anc rpnnen2lem9 breqtrd c2 1lt2
        difss 2re 3pos ltdiv1ii recni div1i cc 3cn ax-1cn 3ne0 pm3.2i divsubdir
        mp3an df-3 eqcomi 2cn subadd2i opreq1i recn 3syl eqtrd cz cseqz cli nnz
        uzss nnuz eleq2s syl6sseqr wf snssi cif sylan nnre ltne dividi 3eqtr3ri
        mpbir 3brtr4i lt01 ltdiv23ii simpli expgt0 ltmul2 mpbii addid2 gt0ne0ii
        mp3an12 divass mp3an23 addid1 ax-1rid 3brtr4d lelttrd cop wex ax-resscn
        eqtr4d rpnnen2lem2 fss wfn ffnfv simprbi sylc peano2nn rpnnen2lem5 fvex
        ssralv isum1p weq fveq2 eleq1 opreq2 eqidd ifbieq12d visset snid iftrue
        syl6eq eqeq12d imbi2d rpnnen2lem1 expcom vtoclga pm2.43i syldan eluzp1l
        wn adantr elsni necon3ai iffalse sumeq2dv serzclim0 elisseti fconstopab
        cxp isumclim4 opreq12d eqbrtrrd ltletrd rpnnen2lem10 necon3d mpd sylbir
        mpdan exp32 rexlimdv ) CKUCZDKUCZUDZGUEZFUEZLMUXQCNUXQDNUJUKGKUFZKEUEZC
        HUGUGZEUHZKUXTDHUGUGZEUHZUIZFCDULZUXPUXRUYFNZUXSUYEUXPUYGUXSUDZUDZAUYEJ
        AUXRVAUGZUYAEUHZUYJUYCEUHZUIZUYEAUYLONZUYKONZUYLUYKLMUYMAUXOUXRKNZUYNAU
        YIUXOJUXNUXOUYHUMUNZAUXNUYGUYPAUYIUXNJUXNUXOUYHUOUNZAUYIUYGJUXPUYGUXSUP
        UNZUYGUXNUXRCNZUYPUXRCDUQZCKUXRURWEUSZBDEHUXRIUTUSZAUXNUYPUYOUYRVUBBCEH
        UXRIUTUSZAUYLPQVBRZUXRVCRZSVDRZUYKVUCAVUFONZSONZVUGONAVUEONZUXRVENZVUHQ
        KNVUJVFQVGVHZAUYPVUKVUBUXRVIVJZVUEUXRVKVLZVMVUFSVNVOZVUDAUYLSVUFVUEVPRZ
        PVUEVQRZVBRZVDRZVUGVUCAVUIVURONZVUSONVMAVUPONZVUQVRNZVUTAVUHVUJVVAVUNVU
        LVUFVUEVSVOVUQPVUEVTVULWAZVUEPLMZSVUQLMSVUELMZVVDQONPQLMVVEVVDUDWBWCQWD
        WFZWGVUEPVULVTWHWIZWJZVUPVUQWKVOZSVURVNVLVUOAUYLUYJUXTKUXRWLZULZHUGUGEU
        HZVUSWPADVVKUCZVVKKUCZUYPUYLVVLWPMAVVMVVJKDULZUCZAVVJUYFVVOAUXRUYFUYSWM
        AUXNUYFVVOUCUYRCKDWNVJWOAUXOVVJKUCZVVMVVPUJUYQAUXRKVUBWMDVVJKWQUSWRVVNA
        KVVJXFWSVUBBDVVKEHUXRIWTXAAUYPVVLVUSTVUBBEHUXRIXBVJXCAVUFVUEVUQVBRZVPRZ
        VUFPVPRZVUSVUGLAVVRPLMZVVSVVTLMZVUEPVBRZVUQLMVWAVUEXDQVBRZVWCVUQLPXDLMV
        UEVWDLMXEPXDQVTXGWBXHXIWIVUEVUEVULXJZXKQPVQRZQVBRZQQVBRZVUEVQRZVWDVUQQX
        LNZPXLNVWJQSUIZUDVWGVWITXMXNVWJVWKXMXOXPQPQXQXRVWFXDQVBVWFXDTXDPVDRZQTQ
        VWLXSXTQPXDXMXNYAYBUUCYCVWHPVUEVQQXMXOUUAYCUUBUUDVUEPVUQVULVTVVCUUEVVGU
        UFWIAVUHSVUFLMZVWAVWBUJZVUNAVUJVUKVVEVWMVUJAVULWSVUMVVEAVVEVVDVVFUUGWSV
        UEUXRUUHXAVVRONZPONVUHVWMUDVWNVUJVVBVWOVULVVHVUEVUQWKWFVTVVRPVUFUUIUUMU
        SUUJAVUSVURVVSAVUTVURXLNVUSVURTVVIVURYDVURUUKYEAVUHVUFXLNZVURVVSTZVUNVU
        FYDZVWPVUEXLNVUQXLNZVUQSUIZUDVWQVWEVWSVWTVUQVVCXJVUQVVCVVGUULXPVUFVUEVU
        QUUNUUOYEYFAVUGVUFVVTAVUHVWPVUGVUFTVUNVWRVUFUUPYEAVUHVVTVUFTVUNVUFUUQVJ
        UVCUURUUSAUYJUXTVVJHUGZUGZEUHZVUGUYKWPAUYPVXCVUGTVUBUYPVXCUXRVXAUGZUXRP
        VDRZVAUGZVXBEUHZVDRZVUGUYPUXRYGNZVXBXLNZEUYJUFZVXEVDUUTZVXAYHRUAUEYIMUA
        UVAZVXCVXHTUXRYJZUYPUYJKUCVXJEKUFZVXKUYPUYJPVAUGZKUYJVXPUCUXRVXPKPUXRYK
        YLYMYLYNUYPKXLVXAYOZVXOUYPKOVXAYOZOXLUCVXQUYPVVQVXRUXRKYPZBVVJEHIUVDVJU
        VBKOXLVXAUVEVOVXQVXAKUVFVXOEKXLVXAUVGUVHVJVXJEUYJKUVMUVIUYPVVQVXEKNZVXM
        VXSUXRUVJZBUAVVJEHVXEIUVKUSUAEVXAUXRVVJHUVLUVNXAUYPVXDVUFVXGSVDUYPVXDVU
        FTZUYPVXBUXTVVJNZVUEUXTVCRZSYQZTZUKUYPVYBUKEUXRKEFUVOZVYFVYBUYPVYGVXBVX
        DVYEVUFUXTUXRVXAUVPVYGVYEUXRVVJNZVUFSYQZVUFVYGVYCVYHVYDSVUFSUXTUXRVVJUV
        QUXTUXRVUEVCUVRVYGSUVSUVTVYHVYIVUFTUXRFUWAUWBVYHVUFSUWCVHUWDUWEUWFUYPUX
        TKNZVYFUYPVVQVYJVYFVXSBVVJEHIUWGYRZUWHUWIUWJUYPVXGVXFSEUHZSUYPVXFVXBSEU
        YPUXTVXFNZUDZVXBVYESUYPVYMVYJVYFUYPVXFKUCZVYMVYJUYPVXTVYOVYAVXTVXFVXPKV
        XFVXPUCVXEVXPKPVXEYKYLYMYLYNVJVXFKUXTURYRZVYKUWKVYNUXTUXRUIZVYCUWMVYEST
        VYNUXRONZUXTONZUXRUXTLMZVYQUYPVYRVYMUXRYSUWNVYNVYJVYSVYPUXTYSVJUYPVXIVY
        MVYTVXNUXRUXTUWLYRUXRUXTYTXAVYCUXTUXRUXTUXRUWOUWPVYCVYDSUWQYEYFUWRUYPVX
        TVXEYGNZVYLSTZVYAVXEYJWUAVXLVXFSWLUXBZYHRSYIMWUBVXEUWSUBSSEWUCVXESOVMUW
        TZEUBVXFSUXAWUDUXCUXKYEYFUXDYFVJAVVJCUCZUXNUYPVXCUYKWPMAUYGUYTWUEUYSVUA
        UXRCYPYEUYRVUBBVVJCEHUXRIWTXAUXEUXFUYLUYKYTXAAUYBUYDUYKUYLABCDEFGHIJUXG
        UXHUXIUXJUXLUXM $.
        $( [13-May-2013] $)
    $}

    $d F k m n x y z $.
    $( The other half of ~ rpnnen , where we show an injection from sets of
       natural numbers to real numbers.  The obvious choice for this is binary
       expansion, but it has the unfortunate property that it does not produce
       an injection on numbers which end with all 0's or all 1's (the more
       well-known decimal version of this is ~ 0.999... ).  Instead, we opt for
       a ternary expansion, which produces (a scaled version of) the Cantor
       set.  Since the Cantor set is riddled with gaps, we can show that any
       two sequences that are not equal must differ somewhere, and when they
       do, they are placed a finite distance apart, thus ensuring that the map
       is injective.

       Our map assigns to each subset ` A ` of the natural numbers the number
       ` sum_ k e. A ( 3 ^ -u k ) = sum_ k e. NN ( ( F `` A ) `` k ) ` , where
       ` ( ( F `` A ) `` k ) = if ( k e. A , ( 3 ^ -u k ) , 0 ) ) `
       ( ~ rpnnen2lem1 ).  This is an infinite sum of real numbers
       ( ~ rpnnen2lem2 ), and since ` A C_ B ` implies
       ` ( F `` A ) <_ ( F `` B ) ` ( ~ rpnnen2lem4 ) and ` ( F `` NN ) `
       converges to ` 1 / 2 ` ( ~ rpnnen2lem3 ) by ~ geoisum1 , the sum is
       convergent to some real ( ~ rpnnen2lem5 and ~ rpnnen2lem6 ) by the
       comparison test for convergence ~ cvgcmpi .  The comparison test also
       tells us that ` A C_ B ` implies ` sum_ ( F `` A ) <_ sum_ ( F `` B ) `
       ( ~ rpnnen2lem7 ).

       Putting it all together, if we have two sets ` x =/= y ` , there must
       differ somewhere, and so there must be an ` m ` such that
       ` A. n < m ( n e. x <-> n e. y ) ` but ` m e. ( x \ y ) ` or vice
       versa.  In this case, we split off the first ` m - 1 ` terms
       ( ~ rpnnen2lem8 ) and cancel them ( ~ rpnnen2lem10 ), since these are
       the same for both sets.  For the remaining terms, we use the subset
       property to establish that
       ` sum_ ( F `` y ) <_ sum_ ( F `` ( NN \ { m } ) ) ` and
       ` sum_ ( F `` { m } ) <_ sum_ ( F `` x ) ` (where these sums are only
       over ` ( ZZ>= `` m ) ` ), and since
       ` sum_ ( F `` ( NN \ { m } ) ) = ( 3 ^ -u m ) / 2 ` ( ~ rpnnen2lem9 )
       and ` sum_ ( F `` { m } ) = ( 3 ^ -u m ) ` , we establish that
       ` sum_ ( F `` y ) < sum_ ( F `` x ) ` ( ~ rpnnen2lem11 ) so that they
       must be different.  By contraposition, we find that this map is an
       injection.  (Contributed by Mario Carneiro, 13-May-2013.) $)
    rpnnen2 $p |- ~P NN ~<_ RR $=
      ( vy vz vn vm cn wcel cr cv cfv wss wa wi wrex c0 syl2an wn cpw cdom nnex
      cvv wbr pwex csu elpwi c1 cuz nnuz sumeq1i 1nn rpnnen2lem6 mpan2 syl5eqel
      syl wceq weq wne clt wel wb wral cdif wo cun cle ssdifss unss biimpi eqss
      ssdif0 anbi12i un00 3bitri necon3bii ex sseld imp wal df-ral con34b eldif
      nnwo orbi12i elun xor 3bitr4ri imbi2i bitri albii alral nnre ltnle ancoms
      con1bii imbi1d ralbidva syl5ibr syl5bi reximdva rexun syl6ib rpnnen2lem11
      syld biid bicom ralbii rexbii necom 3imtr3g jaod necon4d fveq1d sumeq2sdv
      fveq2 impbid1 dom2 ax-mp ) IUAZUDJYAKUBUEIUCUFEFYAKIBLZELZCMZMZBUGZIYBFLZ
      CMZMZBUGZUDYCYAJZYCINZYFKJYCIUHZYLYFUIUJMZYEBUGZKIYNYEBUKULYLUIIJYOKJUMAY
      CBCUIDUNUOUPUQYKYGYAJZOZYFYJUREFUSZYQYCYGYFYJYQYCYGUTZGLZHLZVAUEZGEVBZGFV
      BZVCZPZGIVDZHYCYGVEZQZUUGHYGYCVEZQZVFZYFYJUTZYQYSUUGHUUHUUJVGZQZUULYQYSUU
      AYTVHUEZGUUNVDZHUUNQZUUOYQYSUURYQUUNINZUUNRUTZUURYSYKYLYGINZUUSYPYMYGIUHZ
      YLUUHINZUUJINZUUSUVAYCIYGVIYGIYCVIUVCUVDOUUSUUHUUJIVJVKSSZYSUUTYCYGUUNRYR
      YCYGNZYGYCNZOUUHRURZUUJRURZOUUNRURYCYGVLUVFUVHUVGUVIYCYGVMYGYCVMVNUUHUUJV
      OVPVQVKHGUUNWESVRYQUUQUUGHUUNYQUUAUUNJZOUUAIJZUUQUUGPYQUVJUVKYQUUNIUUAUVE
      VSVTUUQUUPTZUUEPZGWAZUVKUUGUUQYTUUNJZUUPPZGWAUVNUUPGUUNWBUVPUVMGUVPUVLUVO
      TZPUVMUVOUUPWCUVQUUEUVLUUEUVOYTUUHJZYTUUJJZVFUUCUUDTOZUUDUUCTOZVFUVOUUETU
      VRUVTUVSUWAYTYCYGWDYTYGYCWDWFYTUUHUUJWGUUCUUDWHWIWQWJWKWLWKUVNUUGUVKUVMGI
      VDUVMGIWMUVKUUFUVMGIUVKYTIJZOUUBUVLUUEUVKUUAKJZYTKJZUUBUVLVCZUWBUUAWNYTWN
      UWDUWCUWEYTUUAWOWPSWRWSWTXAUQXBXFUUGHUUHUUJXCXDYQUUIUUMUUKYKYLUVAUUIUUMPY
      PYMUVBYLUVAOUUAUUHJUUGOOZAYCYGBHGCDUWFXGXESYQUUBUUDUUCVCZPZGIVDZHUUJQZYJY
      FUTZUUKUUMYPYKUWJUWKPZYPUVAYLUWLYKUVBYMUVAYLOUUAUUJJUWIOOZAYGYCBHGCDUWMXG
      XESWPUWIUUGHUUJUWHUUFGIUWGUUEUUBUUDUUCXHWJXIXJYJYFXKXLXMXFXNYRIYEYIBYRYBY
      DYHYCYGCXQXOXPXRXSXT $.
      $( [13-May-2013] $)
  $}

  ${
    $d j k m n x y $.
    $( The cardinality of the continuum is the same as the powerset of
       ` om ` .  This is a stronger statement than ~ ruc , which only asserts
       that ` RR ` is uncountable, i.e. has a cardinality larger than ` om ` .
       The main proof is in two parts, ~ rpnnen1 and ~ rpnnen2 , each showing
       an injection in one direction, and this last part uses ~ sbth to prove
       that the sets are equinumerous.  By constructing explicit injections, we
       avoid the use of AC, and in fact we could also avoid the use of Infinity
       for trivial reasons (under the negation of Infinity, this case ` ~P om `
       is not a set and ` RR ` is empty), but that would not be very
       enlightening.  (Contributed by Mario Carneiro, 13-May-2013.) $)
    rpnnen $p |- RR ~~ ~P NN $=
      ( vx vm vk vn vy cr cn cdom wbr cen cq cmap co cdiv clt cmpt c2o nnex cc0
      mp2an vj cpw cv cz crab csup opreq1 breq1d cbvrabv opreq2 rabbidv supeq1d
      weq id opreq12d cbvmptv wceq wcel opreq1d adantr mpteq2dva syl5eq rpnnen1
      breq2 csdm qnnen canth2 sdomdom pwex mapdom1 mp2b pw2en enref oprex mapen
      ensdomtr domentr cxp com 2onn elisseti mapxpen xpnnen entri entr4i wel c1
      domtr c3 cexp cif elequ2 ifbid elequ1 eqidd ifbieq12d syl6eq rpnnen2 sbth
      ) FGUBZHIZWTFHIFWTJIFKGLMZHIXBWTHIZXAABUCZCUCZNMZAUCZOIZBUDUEZCDEFUAGXDUA
      UCZNMZEUCZOIZBUDUEZFOUFZXJNMZPZPXHDUCZXENMZXGOIBDUDBDUMXFXSXGOXDXRXENUGUH
      UIEAFXQCGXIFOUFZXENMZPZEAUMZXQCGXFXLOIZBUDUEZFOUFZXENMZPYBUACGXPYGUACUMZX
      OYFXJXENYHFXNYEOYHXMYDBUDYHXKXFXLOXJXEXDNUJUHUKULYHUNUOUPYCCGYGYAYCYGYAUQ
      XEGURYCYFXTXENYCFYEXIOYCYDXHBUDXLXGXFOVDUKULUSUTVAVBUPVCXBQGLMZGLMZHIZYJW
      TJIXCXBWTGLMZHIZYLYJJIZYKKWTVEIZKWTHIYMKGJIGWTVEIYOVFGRVGKGWTVPTKWTVHKWTG
      GRVIRVJVKWTYIJIGGJIYNGRVLZGRVMWTYIGGQGLVNZRVOTXBYLYJVQTYJYIWTYQYJQGGVRZLM
      ZYIQGGQVSVTWAZRRWBQQJIYRGJIYSYIJIQYTVMWCQQYRGYTRVOTWDYPWEXBYJWTVQTFXBWTWH
      TACEWTDGDEWFZWGWINMZXRWJMZSWKZPZPEAWTUUECGCAWFZUUBXEWJMZSWKZPZYCUUEDGDAWF
      ZUUCSWKZPUUIYCDGUUDUUKYCUUDUUKUQXRGURYCUUAUUJUUCSEADWLWMUTVADCGUUKUUHDCUM
      ZUUJUUFUUCSUUGSDCAWNXRXEUUBWJUJUULSWOWPUPWQUPWRFWTWST $.
      $( [13-May-2013] $)
  $}

  $( The set of natural numbers is strictly dominated by the set of real
     numbers, i.e. the real numbers are uncountable.  This proof is a simple
     corollary of ~ rpnnen , which determines the exact cardinality of the
     reals.  For an alternate proof discussed at
     ~ http://us.metamath.org/mpegif/mmcomplex.html#uncountable , see
     ~ ruc . $)
  rucALT $p |- NN ~< RR $=
    ( cn cpw csdm wbr cr cen nnex canth2 pwex rpnnen ensymi cvv wcel wa wi reex
    sdomentr ax-mp mp2an ) AABZCDZTEFDZAECDZAGHETAGIJKELMUAUBNUCOPATELQRS $.
    $( [13-May-2013] $) $( [13-Oct-2004] $)

  ${
    ruclem1.a $e |- A e. RR $.
    ruclem1.b $e |- B e. RR $.
    $( Lemma for ~ ruc (the reals are uncountable).  This is an arithmetic fact
       that will be used to compute ordering relations. $)
    ruclem1 $p |- ( A < B <-> A < ( ( ( 2 x. A ) + B ) / 3 ) ) $=
      ( clt wbr c3 cmul co c2 caddc cdiv 2re remulcli ltadd2i recni breq1i 3pos
      c1 3re df-3 opreq1i ax-1cn adddiri mulid2i opreq2i 3eqtrri bitri readdcli
      2cn ltdiv1ii gt0ne0ii divcan3i 3bitri ) ABEFZGAHIZJAHIZBKIZEFZUPGLIZURGLI
      ZEFAVAEFUOUQAKIZUREFUSABUQCDJAMCNZOVBUPUREUPJSKIZAHIUQSAHIZKIVBGVDAHUAUBJ
      SAUJUCACPZUDVEAUQKAVFUEUFUGQUHUPURGGATCNUQBVCDUITRUKUTAVAEAGVFGTPGTRULUMQ
      UN $.
      $( [14-Oct-2004] $)

    $( Lemma for ~ ruc .  Arithmetic fact that will be used to compute ordering
       relations. $)
    ruclem2 $p |- ( A < B <->
                  ( ( ( 2 x. A ) + B ) / 3 ) < ( ( A + ( 2 x. B ) ) / 3 ) ) $=
      ( clt wbr caddc co c2 cmul c3 cdiv readdcli ltadd1i recni 2timesi opreq1i
      eqtri 2re remulcli addassi opreq2i add12i breq12i 3pos ltdiv1ii 3bitr2i
      3re ) ABEFAABGHZGHZBUIGHZEFIAJHZBGHZAIBJHZGHZEFUMKLHUOKLHEFABUICDABCDMNUM
      UJUOUKEUMAAGHZBGHUJULUPBGAACOZPQAABUQUQBDOZUARUOABBGHZGHUKUNUSAGBURPUBABB
      UQURURUCRUDUMUOKULBIASCTDMAUNCIBSDTMUHUEUFUG $.
      $( [14-Oct-2004] $)

    $( Lemma for ~ ruc .  Arithmetic fact that will be used to compute ordering
       relations. $)
    ruclem3 $p |- ( A < B <-> ( ( A + ( 2 x. B ) ) / 3 ) < B ) $=
      ( clt wbr c2 cmul co caddc c3 remulcli c1 2cn ax-1cn opreq1i recni breq2i
      cdiv 3re 2re ltadd1i addcomi eqtri adddiri mulid2i 3eqtrri bitri readdcli
      df-3 3pos ltdiv1ii gt0ne0ii divcan3i 3bitri ) ABEFZAGBHIZJIZKBHIZEFZURKSI
      ZUSKSIZEFVABEFUPURBUQJIZEFUTABUQCDGBUADLZUBVCUSUREUSMGJIZBHIMBHIZUQJIVCKV
      EBHKGMJIVEUJGMNOUCUDPMGBONBDQZUEVFBUQJBVGUFPUGRUHURUSKAUQCVDUIKBTDLTUKULV
      BBVAEBKVGKTQKTUKUMUNRUO $.
      $( [14-Oct-2004] $)
  $}

  $( Lemma for ~ ruc .  Helper lemma showing a tedious equality used several
     times. $)
  ruclem4 $p |- ( ( A = C /\ B = D ) ->
                if ( ( ( 1st ` A ) < B /\ B < ( 2nd ` A ) ) ,
                 <. ( ( ( 2 x. B ) + ( 2nd ` A ) ) / 3 ) ,
                    ( ( B + ( 2 x. ( 2nd ` A ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` A ) ) + ( 2nd ` A ) ) / 3 ) ,
                    ( ( ( 1st ` A ) + ( 2 x. ( 2nd ` A ) ) ) / 3 ) >. ) =
                if ( ( ( 1st ` C ) < D /\ D < ( 2nd ` C ) ) ,
                 <. ( ( ( 2 x. D ) + ( 2nd ` C ) ) / 3 ) ,
                    ( ( D + ( 2 x. ( 2nd ` C ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` C ) ) + ( 2nd ` C ) ) / 3 ) ,
                    ( ( ( 1st ` C ) + ( 2 x. ( 2nd ` C ) ) ) / 3 ) >. ) ) $=
    ( wceq c1st cfv clt wbr c2nd c2 cmul co caddc cdiv cop cif opreq2d opreq1d
    c3 fveq2 breq1d breq2d anbi12d ifbid opeq12d opreq12d ifeq12 syl11anc eqtrd
    wa breq2 breq1 opreq2 opreq1 ifeq1d sylan9eq ) ACEZBDEZAFGZBHIZBAJGZHIZUKZK
    BLMZVBNMZTOMZBKVBLMZNMZTOMZPZKUTLMZVBNMZTOMZUTVHNMZTOMZPZQZCFGZBHIZBCJGZHIZ
    UKZVEWANMZTOMZBKWALMZNMZTOMZPZKVSLMZWANMZTOMZVSWFNMZTOMZPZQZVSDHIZDWAHIZUKZ
    KDLMZWANMZTOMZDWFNMZTOMZPZWOQZURVRWCVKVQQZWPURVDWCVKVQURVAVTVCWBURUTVSBHACF
    UAZUBURVBWABHACJUAZUCUDUEURVKWIEVQWOEXGWPEURVGWEVJWHURVFWDTOURVBWAVENXIRSUR
    VIWGTOURVHWFBNURVBWAKLXIRZRSUFURVNWLVPWNURVMWKTOURVLWJVBWANURUTVSKLXHRXIUGS
    URVOWMTOURUTVSVHWFNXHXJUGSUFWCVKWIVQWOUHUIUJUSWPWSWIWOQXFUSWCWSWIWOUSVTWQWB
    WRBDVSHULBDWAHUMUDUEUSWSWIXEWOUSWEXBWHXDUSWDXATOUSVEWTWANBDKLUNSSUSWGXCTOBD
    WFNUOSUFUPUJUQ $.
    $( [9-Oct-2004] $)

  ${
    ruclem5.0 $e |- F : NN --> RR $.
    ruclem5.1 $e |- C = ( { <. 1 , <. ( ( F ` 1 ) + 1 ) ,
                     ( ( F ` 1 ) + 2 ) >. >. } u. ( F |` ( NN \ { 1 } ) ) ) $.
    $( Lemma for ~ ruc .  Helper lemma showing the input function used for our
       recursive sequence builder (defined in ~ ruclem13 ) is a set. $)
    ruclem5 $p |- C e. _V $=
      ( c1 cfv caddc co c2 cop csn cn cdif cres cun cvv snex wcel cr wf eqeltri
      nnex fex mp2an resexg ax-mp unex ) AEEBFZEGHUHIGHJJZKZBLEKMZNZOPDUJULUIQB
      PRZULPRLSBTLPRUMCUBLSPBUCUDBUKPUEUFUGUA $.
      $( [7-Oct-2004] $)

    $( Lemma for ~ ruc .  Helper lemma showing the input function used for our
       recursive sequence builder (defined in ~ ruclem13 ) matches our input
       mapping ` F ` for successor values. $)
    ruclem6 $p |- ( C |` ( NN \ { 1 } ) ) = ( F |` ( NN \ { 1 } ) ) $=
      ( cn c1 csn cdif cres cfv caddc co c2 cop cun wceq reseq1 ax-mp c0 3eqtri
      resundir cin disjdif wfn wb 1nn elisseti fnsn fnresdisj mpbi residm uncom
      uneq12i un0 ) AEFGZHZIZFFBJZFKLURMKLNZNGZBUPIZOZUPIZUTUPIZVAUPIZOZVAAVBPU
      QVCPDAVBUPQRUTVAUPUAVFSVAOVASOVAVDSVEVAUOUPUBSPZVDSPZUOEUCUTUOUDVGVHUEFUS
      FEUFUGUHUOUPUTUIRUJBUPUKUMSVAULVAUNTT $.
      $( [9-Oct-2004] $)

    $( Lemma for ~ ruc .  Helper lemma showing the initial value of the input
       function for our recursive sequence builder (defined in ~ ruclem13 ). $)
    ruclem7 $p |- ( C ` 1 ) =
                     <. ( ( F ` 1 ) + 1 ) , ( ( F ` 1 ) + 2 ) >. $=
      ( c1 cfv caddc co c2 cop cn 1nn elisseti opex fvsnun1 ) EEBFZEGHZPIGHZJKB
      AEKLMQRNDO $.
      $( [7-Oct-2004] $)

    ${
      ruclem8.a $e |- A e. NN $.
      $( Lemma for ~ ruc .  Helper lemma showing the successor value of the
         input function for our recursive sequence builder (defined in
         ~ ruclem13 ). $)
      ruclem8 $p |- ( C ` ( A + 1 ) ) = ( F ` ( A + 1 ) ) $=
        ( c1 caddc co cn csn cdif wcel cfv wceq ax-mp clt wbr cc0 mpbi peano2nn
        wne eldifsn nngt0i 0re nnrei 1re ltadd1i ax-1cn addid2i breq1i bitri wb
        nngt1ne1 mpbir2an c2 cop 1nn elisseti opex fvsnun2 ) AGHIZJGKLMZVBBNVBC
        NOVCVBJMZVBGUBZVBJGUCAJMVDFAUAPZGVBQRZVESAQRZVGAFUDVHSGHIZVBQRVGSAGUEAF
        UFUGUHVIGVBQGUIUJUKULTVDVGVEUMVFVBUNPTUOGGCNZGHIZVJUPHIZUQJVBCBGJURUSVK
        VLUTEVAP $.
        $( [14-Oct-2004] $)
    $}
  $}

  ${
    $d x y z $.  $d z A $.
    ruclem9.2 $e |- D = { <. <. x , y >. , z >. |
             ( ( x e. ( RR X. RR ) /\ y e. RR ) /\ z = A ) } $.
    $( Lemma for ~ ruc .  Helper lemma showing the operation used for our
       recursive sequence builder (defined in ~ ruclem13 ) is a set. $)
    ruclem9 $p |- D e. _V $=
      ( cr cxp reex xpex oprabex2 ) ABCGGHGDEGGIIJIFK $.
      $( [7-Oct-2004] $)
  $}

  ${
    $d x y $.
    ruclem10.a $e |- A e. NN $.
    ruclem.d $e |- D e. _V $.
    ruclem.c $e |- C e. _V $.
    ruclem10.3 $e |- G = ( 1st o. ( D seq1 C ) ) $.
    $( Lemma for ~ ruc .  The values of our recursive sequence builder are
       pairs of real numbers.  The values of our constructed function ` G ` are
       the first of these pairs. $)
    ruclem10 $p |- ( 1st ` ( ( D seq1 C ) ` A ) ) = ( G ` A ) $=
      ( cfv c1st cseq1 co ccom fveq1i wfun cn wfn wcel wceq cvv wfo fo1st fofun
      ax-mp seq1fn fvco2 mp3an eqtr2i ) ADIAJCBKLZMZIZAUIIJIZADUJHNJOZUIPQAPRUK
      ULSTTJUAUMUBTTJUCUDCBFGUEEPAJUIUFUGUH $.
      $( [14-Oct-2004] $)
  $}

  ${
    $d x y $.
    ruclem11.a $e |- A e. NN $.
    ruclem11.d $e |- D e. _V $.
    ruclem11.c $e |- C e. _V $.
    ruclem11.4 $e |- H = ( 2nd o. ( D seq1 C ) ) $.
    $( Lemma for ~ ruc .  The values of our recursive sequence builder are
       pairs of real numbers.  The values of our constructed function ` H ` are
       the second of these pairs. $)
    ruclem11 $p |- ( 2nd ` ( ( D seq1 C ) ` A ) ) = ( H ` A ) $=
      ( cfv c2nd cseq1 co ccom fveq1i wfun cn wfn wcel wceq cvv wfo fo2nd fofun
      ax-mp seq1fn fvco2 mp3an eqtr2i ) ADIAJCBKLZMZIZAUIIJIZADUJHNJOZUIPQAPRUK
      ULSTTJUAUMUBTTJUCUDCBFGUEEPAJUIUFUGUH $.
      $( [14-Oct-2004] $)
  $}

  ${
    $d x y z w v u $.
    ruclem12.2 $e |- D = { <. <. x , y >. , z >. |
             ( ( x e. ( RR X. RR ) /\ y e. RR ) /\
             z = if ( ( ( 1st ` x ) < y /\ y < ( 2nd ` x ) ) ,
                 <. ( ( ( 2 x. y ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( y + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` x ) ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( ( 1st ` x ) + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ) ) } $.
    $( Lemma for ~ ruc .  A helper lemma that changes bound variables. $)
    ruclem12 $p |- D = { <. <. w , v >. , u >. |
             ( ( w e. ( RR X. RR ) /\ v e. RR ) /\
             u = if ( ( ( 1st ` w ) < v /\ v < ( 2nd ` w ) ) ,
                 <. ( ( ( 2 x. v ) + ( 2nd ` w ) ) / 3 ) ,
                    ( ( v + ( 2 x. ( 2nd ` w ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` w ) ) + ( 2nd ` w ) ) / 3 ) ,
                    ( ( ( 1st ` w ) + ( 2 x. ( 2nd ` w ) ) ) / 3 ) >. ) ) } $=
      ( cv cr wcel wa cfv c2 cmul co caddc c3 cdiv wceq cxp c1st clt wbr copab2
      c2nd cop cif eleq1 bi2anan9 ruclem4 eqeq2d anbi12d cbvoprab12v cbvoprab3v
      eqeq1 anbi2d 3eqtri ) GAIZJJUAZKZBIZJKZLZCIZUSUBMZVBUCUDVBUSUFMZUCUDLNVBO
      PVGQPRSPVBNVGOPZQPRSPUGNVFOPVGQPRSPVFVHQPRSPUGUHZTZLZABCUEDIZUTKZEIZJKZLZ
      VEVLUBMZVNUCUDVNVLUFMZUCUDLNVNOPVRQPRSPVNNVROPZQPRSPUGNVQOPVRQPRSPVQVSQPR
      SPUGUHZTZLZDECUEVPFIZVTTZLZDEFUEHVKWBABCDEUSVLTZVBVNTZLZVDVPVJWAWFVAVMWGV
      CVOUSVLUTUIVBVNJUIUJWHVIVTVEUSVBVLVNUKULUMUNWBWEDECFVEWCTWAWDVPVEWCVTUPUQ
      UOUR $.
      $( [8-Oct-2004] $)
  $}

  ${
    $d x y z w v u $.  $d w v u D $.
    ruclem13.0 $e |- F : NN --> RR $.
    ruclem13.1 $e |- C = ( { <. 1 , <. ( ( F ` 1 ) + 1 ) ,
                     ( ( F ` 1 ) + 2 ) >. >. } u. ( F |` ( NN \ { 1 } ) ) ) $.
    ruclem13.2 $e |- D = { <. <. x , y >. , z >. |
             ( ( x e. ( RR X. RR ) /\ y e. RR ) /\
             z = if ( ( ( 1st ` x ) < y /\ y < ( 2nd ` x ) ) ,
                 <. ( ( ( 2 x. y ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( y + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` x ) ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( ( 1st ` x ) + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ) ) } $.
    $( Lemma for ~ ruc .  A helper lemma showing the recursive sequence builder
       used for our construction maps natural numbers to pairs of reals. $)
    ruclem13 $p |- ( D seq1 C ) : NN --> ( RR X. RR ) $=
      ( cn cr co wf wa c2 caddc c3 c1 wcel 3re cxp wfn crn wss df-f cv c1st cfv
      cseq1 clt wbr c2nd cmul cdiv cop cif ruclem9 ruclem5 seq1fn csn cdif cres
      ruclem7 oprex opelxp 1nn ffvelrn 1re readdcli 2re mpbir2an eqeltri fssres
      mp2an difss ruclem6 feq1i mpbir wceq elxp6 remulcl mpan readdcl sylan cc0
      wi wne gt0ne0ii redivcl mp3an23 syl sylan2 opelxpi syl11anc ancoms iftrue
      3pos adantll eleq1d syl5ibr iffalse adantrd pm2.61i sylanb foprab seq1rn2
      wn mp3an ) JKKUAZEDUILZMXJJUBXJUCXIUDZJXIXJUEEDABCAUFZUGUHZBUFZUJUKXNXLUL
      UHZUJUKNZOXNUMLZXOPLZQUNLZXNOXOUMLZPLZQUNLZUOZOXMUMLZXOPLZQUNLZXMXTPLZQUN
      LZUOZUPZEIUQZDFGHURZUSRDUHZXISJRUTZVAZKDYOVBZMZXIKUAXIEMXKYMRFUHZRPLZYROP
      LZUOZXIDFGHVCUUAXISYSKSYTKSYSYTKKYROPVDVEYRRJKFMZRJSYRKSGVFJKRFVGVNZVHVIY
      ROUUCVJVIVKVLYQYOKFYOVBZMZUUBYOJUDUUEGJYNVOJKYOFVMVNYOKYPUUDDFGHVPVQVRABC
      XIKYJXIEIXLXISXLXMXOUOVSZXMKSZXOKSZNZNXNKSZYJXISZXLKKVTUUIUUJUUKUUFXPUUIU
      UJNZUUKWFUULUUKXPYCXISZUUHUUJUUMUUGUUJUUHUUMUUJUUHNZXSKSZYBKSZUUMUUNXRKSZ
      UUOUUJXQKSZUUHUUQOKSZUUJUURVJOXNWAWBXQXOWCWDUUQQKSZQWEWGZUUOTQTWQWHZXRQWI
      WJWKUUNYAKSZUUPUUHUUJXTKSZUVCUUSUUHUVDVJOXOWAWBZXNXTWCWLUVCUUTUVAUUPTUVBY
      AQWIWJWKXSYBKKWMWNWOWRXPYJYCXIXPYCYIWPWSWTXPXGZUUIUUKUUJUUIUUKUVFYIXISZUU
      IYFKSZYHKSZUVGUUIYEKSZUVHUUGYDKSZUUHUVJUUSUUGUVKVJOXMWAWBYDXOWCWDUVJUUTUV
      AUVHTUVBYEQWIWJWKUUIYGKSZUVIUUHUUGUVDUVLUVEXMXTWCWLUVLUUTUVAUVITUVBYGQWIW
      JWKYFYHKKWMWNUVFYJYIXIXPYCYIXAWSWTXBXCWRXDXEXIKEDYKYLXFXHVK $.
      $( [14-Oct-2004] $)
  $}

  ${
    $d x y z w v u $.  $d A w v u $.  $d C w v u $.  $d D w v u $.
    $d G w v u $.  $d H w v u $.  $d F z w v u $.  $d B w v $.
    ruclem.0 $e |- F : NN --> RR $.
    ruclem.1 $e |- C = ( { <. 1 , <. ( ( F ` 1 ) + 1 ) ,
                     ( ( F ` 1 ) + 2 ) >. >. } u. ( F |` ( NN \ { 1 } ) ) ) $.
    ruclem.2 $e |- D = { <. <. x , y >. , z >. |
             ( ( x e. ( RR X. RR ) /\ y e. RR ) /\
             z = if ( ( ( 1st ` x ) < y /\ y < ( 2nd ` x ) ) ,
                 <. ( ( ( 2 x. y ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( y + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` x ) ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( ( 1st ` x ) + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ) ) } $.
    ruclem.3 $e |- G = ( 1st o. ( D seq1 C ) ) $.
    ruclem.4 $e |- H = ( 2nd o. ( D seq1 C ) ) $.
    $( Lemma for ~ ruc .  A helper lemma showing the initial value of the
       recursive sequence builder used for our construction. $)
    ruclem14 $p |- ( ( D seq1 C ) ` 1 ) =
               <. ( ( F ` 1 ) + 1 ) , ( ( F ` 1 ) + 2 ) >. $=
      ( c1 co cfv caddc c2 c3 cdiv cseq1 cop c1st clt wbr c2nd cmul cif ruclem9
      cv wa ruclem5 seq11 ruclem7 eqtri ) NEDUAOPNDPNFPZNQOUPRQOUBEDABCAUJZUCPZ
      BUJZUDUEUSUQUFPZUDUEUKRUSUGOUTQOSTOUSRUTUGOZQOSTOUBRURUGOUTQOSTOURVAQOSTO
      UBUHEKUIDFIJULUMDFIJUNUO $.
      $( [8-Oct-2004] $)

    ${
      ruclem15.a $e |- A e. NN $.
      $( Lemma for ~ ruc .  A helper lemma showing the successor value of the
         recursive sequence builder used for our construction. $)
      ruclem15 $p |- ( ( D seq1 C ) ` ( A + 1 ) ) =
  if ( ( ( G ` A ) < ( F ` ( A + 1 ) ) /\
   ( F ` ( A + 1 ) ) < ( H ` A ) ) ,
  <. ( ( ( 2 x. ( F ` ( A + 1 ) ) ) + ( H ` A ) ) / 3 ) ,
    ( ( ( F ` ( A + 1 ) ) + ( 2 x. ( H ` A ) ) ) / 3 ) >. ,
  <. ( ( ( 2 x. ( G ` A ) ) +
      ( H ` A ) ) / 3 ) ,
    ( ( ( G ` A ) +
      ( 2 x. ( H ` A ) ) ) / 3 ) >. ) $=
        ( caddc co c2 c3 cdiv vw vv vu cseq1 cfv c1st clt wbr c2nd cmul cop cif
        c1 wa cn wcel wceq cv ruclem9 ruclem5 seq1p1 ax-mp cxp ruclem13 ffvelrn
        cr mp2an ruclem8 peano2nn eqeltri opex ifex eqid ruclem4 mpan2 ruclem12
        wf mpan oprabval2 wb ruclem10 breq12i ruclem11 anbi12i opreq2i opreq12i
        ifbi opreq1i opeq12i ifeq12 eqtri 3eqtri ) DUMPQZFEUDQZUEZDWNUEZWMEUEZF
        QZWPUFUEZWQUGUHZWQWPUIUEZUGUHZUNZRWQUJQZXAPQZSTQZWQRXAUJQZPQZSTQZUKZRWS
        UJQZXAPQZSTQZWSXGPQZSTQZUKZULZDHUEZWMGUEZUGUHZXSDIUEZUGUHZUNZRXSUJQZYAP
        QZSTQZXSRYAUJQZPQZSTQZUKZRXRUJQZYAPQZSTQZXRYGPQZSTQZUKZULZDUOUPZWOWRUQO
        DFEABCAURZUFUEZBURZUGUHUUAYSUIUEZUGUHUNRUUAUJQUUBPQSTQUUARUUBUJQZPQSTQU
        KRYTUJQUUBPQSTQYTUUCPQSTQUKULFLUSZEGJKUTZVAVBWPVFVFVCZUPZWQVFUPWRXQUQUO
        UUFWNVQYRUUGABCEFGJKLVDOUOUUFDWNVEVGWQXSVFDEGJKOVHZUOVFGVQWMUOUPZXSVFUP
        JYRUUIODVIVBUOVFWMGVEVGVJUAUBUCWPWQUUFVFUAURZUFUEZUBURZUGUHUULUUJUIUEZU
        GUHUNRUULUJQZUUMPQSTQUULRUUMUJQZPQSTQUKRUUKUJQUUMPQSTQUUKUUOPQSTQUKULZX
        QFWSUULUGUHUULXAUGUHUNUUNXAPQSTQUULXGPQSTQUKXPULZXCXJXPXFXIVKXMXOVKVLUU
        JWPUQUULUULUQUUPUUQUQUULVMUUJUULWPUULVNVOWPWPUQUULWQUQUUQXQUQWPVMWPUULW
        PWQVNVRABCUAUBUCFLVPVSVGXQYCXJXPULZYQXCYCVTXQUURUQWTXTXBYBWSXRWQXSUGDEF
        HOUUDUUEMWAZUUHWBWQXSXAYAUGUUHDEFIOUUDUUENWCZWBWDXCYCXJXPWGVBXJYJUQXPYP
        UQUURYQUQXFYFXIYIXEYESTXDYDXAYAPWQXSRUJUUHWEUUTWFWHXHYHSTWQXSXGYGPUUHXA
        YARUJUUTWEZWFWHWIXMYMXOYOXLYLSTXKYKXAYAPWSXRRUJUUSWEUUTWFWHXNYNSTWSXRXG
        YGPUUSUVAWFWHWIYCXJYJXPYPWJVGWKWL $.
        $( [15-Oct-2004] $)
    $}

    $( Lemma for ~ ruc .  A helper lemma showing the initial value of our
       constructed ` G ` . $)
    ruclem16 $p |- ( G ` 1 ) = ( ( F ` 1 ) + 1 ) $=
      ( c1 co cfv c1st caddc c2 c3 cseq1 cop ruclem14 fveq2i 1nn cv clt c2nd wa
      wbr cmul cdiv cif ruclem9 ruclem5 ruclem10 oprex op1st 3eqtr3i ) NEDUAOPZ
      QPNFPZNROZVASROZUBZQPNGPVBUTVDQABCDEFGHIJKLMUCUDNDEGUEABCAUFZQPZBUFZUGUJV
      GVEUHPZUGUJUISVGUKOVHROTULOVGSVHUKOZROTULOUBSVFUKOVHROTULOVFVIROTULOUBUME
      KUNDFIJUOLUPVBVCVANRUQURUS $.
      $( [9-Oct-2004] $)

    $( Lemma for ~ ruc .  A helper lemma showing our constructed function ` G `
       maps ` NN ` to real numbers. $)
    ruclem17 $p |- G : NN --> RR $=
      ( cn cr c1st cxp ccom wf ax-mp cres cseq1 co f1stres ruclem13 fco crn wss
      mp2an wceq frn cores eqtr4i feq1i mpbi ) NOPOOQZUAZEDUBUCZRZSZNOGSUPOUQSN
      UPURSZUTOOUDABCDEFIJKUEZNUPOUQURUFUINOUSGUSPURRZGURUGUPUHZUSVCUJVAVDVBNUP
      URUKTPURUPULTLUMUNUO $.
      $( [15-Oct-2004] $)

    ${
      ruclem18.a $e |- A e. NN $.
      $( Lemma for ~ ruc .  The value of our constructed function ` G ` when
         the value of the input function ` F ` lies between the previous values
         of ` G ` and ` H ` .  This assignment to ` G ` defines a new interval
         between ` G ` and ` H ` (see also ~ ruclem19 ) that avoids the value
         of ` F ` . $)
      ruclem18 $p |- (
       ( ( G ` A ) < ( F ` ( A + 1 ) ) /\ ( F ` ( A + 1 ) ) < ( H ` A ) ) ->
   ( G ` ( A + 1 ) ) = ( ( ( 2 x. ( F ` ( A + 1 ) ) ) + ( H ` A ) ) / 3 ) ) $=
        ( cfv caddc co c3 cdiv c1 clt wbr wa cseq1 c1st c2 cmul ruclem15 iftrue
        cop cif syl5eq fveq2d wcel peano2nn ax-mp c2nd ruclem9 ruclem5 ruclem10
        cn cv oprex op1st 3eqtr3g ) DHPZDUAQRZGPZUBUCVIDIPZUBUCUDZVHFEUERPZUFPU
        GVIUHRVJQRZSTRZVIUGVJUHRZQRSTRZUKZUFPVHHPVNVKVLVQUFVKVLVKVQUGVGUHRVJQRS
        TRVGVOQRSTRUKZULVQABCDEFGHIJKLMNOUIVKVQVRUJUMUNVHEFHDVBUOVHVBUOODUPUQAB
        CAVCZUFPZBVCZUBUCWAVSURPZUBUCUDUGWAUHRWBQRSTRWAUGWBUHRZQRSTRUKUGVTUHRWB
        QRSTRVTWCQRSTRUKULFLUSEGJKUTMVAVNVPVMSTVDVEVF $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  The value of our constructed function ` H ` when
         the value of the input function ` F ` lies between the previous values
         of ` G ` and ` H ` .  This assignment to ` H ` defines a new interval
         between ` G ` and ` H ` (see also ~ ruclem18 ) that avoids the value
         of ` F ` . $)
      ruclem19 $p |- (
       ( ( G ` A ) < ( F ` ( A + 1 ) ) /\ ( F ` ( A + 1 ) ) < ( H ` A ) ) ->
   ( H ` ( A + 1 ) ) = ( ( ( F ` ( A + 1 ) ) + ( 2 x. ( H ` A ) ) ) / 3 ) ) $=
        ( cfv caddc co c3 cdiv c1 clt wbr wa cseq1 c2nd c2 cmul ruclem15 iftrue
        cop cif syl5eq fveq2d wcel peano2nn ax-mp c1st ruclem9 ruclem5 ruclem11
        cn cv oprex op2nd 3eqtr3g ) DHPZDUAQRZGPZUBUCVIDIPZUBUCUDZVHFEUERPZUFPU
        GVIUHRVJQRZSTRZVIUGVJUHRZQRZSTRZUKZUFPVHIPVQVKVLVRUFVKVLVKVRUGVGUHRVJQR
        STRVGVOQRSTRUKZULVRABCDEFGHIJKLMNOUIVKVRVSUJUMUNVHEFIDVBUOVHVBUOODUPUQA
        BCAVCZURPZBVCZUBUCWBVTUFPZUBUCUDUGWBUHRWCQRSTRWBUGWCUHRZQRSTRUKUGWAUHRW
        CQRSTRWAWDQRSTRUKULFLUSEGJKUTNVAVNVQVMSTVDVPSTVDVEVF $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  The value of our constructed function ` G ` when
         the value of the input function ` F ` does not lie between the
         previous values of ` G ` and ` H ` .  This assignment to ` G ` just
         shrinks the interval between ` G ` and ` H ` by some arbitrary
         amount. $)
      ruclem20 $p |- (
    -. ( ( G ` A ) < ( F ` ( A + 1 ) ) /\ ( F ` ( A + 1 ) ) < ( H ` A ) ) ->
   ( G ` ( A + 1 ) ) = ( ( ( 2 x. ( G ` A ) ) + ( H ` A ) ) / 3 ) ) $=
        ( cfv caddc co c3 cdiv c1 clt wbr wa wn cseq1 c1st c2 cmul cop ruclem15
        cif iffalse syl5eq fveq2d cn peano2nn ax-mp cv ruclem9 ruclem5 ruclem10
        wcel c2nd oprex op1st 3eqtr3g ) DHPZDUAQRZGPZUBUCVJDIPZUBUCUDZUEZVIFEUF
        RPZUGPUHVHUIRVKQRZSTRZVHUHVKUIRZQRSTRZUJZUGPVIHPVPVMVNVSUGVMVNVLUHVJUIR
        VKQRSTRVJVQQRSTRUJZVSULVSABCDEFGHIJKLMNOUKVLVTVSUMUNUOVIEFHDUPVCVIUPVCO
        DUQURABCAUSZUGPZBUSZUBUCWCWAVDPZUBUCUDUHWCUIRWDQRSTRWCUHWDUIRZQRSTRUJUH
        WBUIRWDQRSTRWBWEQRSTRUJULFLUTEGJKVAMVBVPVRVOSTVEVFVG $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  The value of our constructed function ` H ` when
         the value of the input function ` F ` does not lie between the
         previous values of ` G ` and ` H ` .  This assignment to ` H ` just
         shrinks the interval between ` G ` and ` H ` by some arbitrary
         amount. $)
      ruclem21 $p |- (
    -. ( ( G ` A ) < ( F ` ( A + 1 ) ) /\ ( F ` ( A + 1 ) ) < ( H ` A ) ) ->
   ( H ` ( A + 1 ) ) = ( ( ( G ` A ) + ( 2 x. ( H ` A ) ) ) / 3 ) ) $=
        ( cfv caddc co c3 cdiv c1 clt wbr wa wn cseq1 c2nd c2 cmul cop ruclem15
        cif iffalse syl5eq fveq2d cn peano2nn ax-mp cv ruclem9 ruclem5 ruclem11
        wcel c1st oprex op2nd 3eqtr3g ) DHPZDUAQRZGPZUBUCVJDIPZUBUCUDZUEZVIFEUF
        RPZUGPUHVHUIRVKQRZSTRZVHUHVKUIRZQRZSTRZUJZUGPVIIPVSVMVNVTUGVMVNVLUHVJUI
        RVKQRSTRVJVQQRSTRUJZVTULVTABCDEFGHIJKLMNOUKVLWAVTUMUNUOVIEFIDUPVCVIUPVC
        ODUQURABCAUSZVDPZBUSZUBUCWDWBUGPZUBUCUDUHWDUIRWEQRSTRWDUHWEUIRZQRSTRUJU
        HWCUIRWEQRSTRWCWFQRSTRUJULFLUTEGJKVANVBVPVSVOSTVEVRSTVEVFVG $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  Each value of our constructed function ` G ` is a
         real number. $)
      ruclem22 $p |- ( G ` A ) e. RR $=
        ( cn cr wf wcel cfv ruclem17 ffvelrn mp2an ) PQHRDPSDHTQSABCEFGHIJKLMNU
        AOPQDHUBUC $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  Each value of our constructed function ` H ` is a
         real number. $)
      ruclem23 $p |- ( H ` A ) e. RR $=
        ( co cfv cr caddc c3 cseq1 c2nd cv c1st clt wbr wa c2 cmul cdiv cop cif
        ruclem9 ruclem5 ruclem11 wcel cn wf ruclem13 ffvelrn mp2an wceq simprbi
        cxp elxp6 ax-mp simpri eqeltrri ) DFEUAPZQZUBQZDIQRDEFIOABCAUCZUDQZBUCZ
        UEUFVNVLUBQZUEUFUGUHVNUIPVOSPTUJPVNUHVOUIPZSPTUJPUKUHVMUIPVOSPTUJPVMVPS
        PTUJPUKULFLUMEGJKUNNUOVJUDQZRUPZVKRUPZVJRRVDZUPZVRVSUGZUQVTVIURDUQUPWAA
        BCEFGJKLUSOUQVTDVIUTVAWAVJVQVKUKVBWBVJRRVEVCVFVGVH $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  A helper lemma for the induction hypothesis in
         ~ ruclem25 . $)
      ruclem24 $p |- ( ( G ` A ) < ( H ` A ) ->
                       ( G ` ( A + 1 ) ) < ( H ` ( A + 1 ) ) ) $=
        ( cfv caddc co clt wbr c1 wa wi c2 cmul c3 cdiv cn cr wf peano2nn ax-mp
        wcel ffvelrn mp2an ruclem23 ruclem2 biimpi adantl ruclem18 ruclem19 a1d
        3brtr4d wn ruclem20 ruclem21 breq12d ruclem22 syl6rbbr biimpd pm2.61i )
        DHPZDUAQRZGPZSTZVNDIPZSTZUBZVLVPSTZVMHPZVMIPZSTZUCVRWBVSVRUDVNUERVPQRUF
        UGRZVNUDVPUERZQRUFUGRZVTWASVQWCWESTZVOVQWFVNVPUHUIGUJVMUHUMZVNUIUMJDUHU
        MWGODUKULUHUIVMGUNUOABCDEFGHIJKLMNOUPZUQURUSABCDEFGHIJKLMNOUTABCDEFGHIJ
        KLMNOVAVCVBVRVDZVSWBWIWBUDVLUERVPQRUFUGRZVLWDQRUFUGRZSTVSWIVTWJWAWKSABC
        DEFGHIJKLMNOVEABCDEFGHIJKLMNOVFVGVLVPABCDEFGHIJKLMNOVHWHUQVIVJVK $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  At any index ` A ` , the value of ` G ` is less
         than the value of ` H ` . $)
      ruclem25 $p |- ( G ` A ) < ( H ` A ) $=
        ( cfv clt wbr c1 co vw vv cn wcel caddc wceq fveq2 breq12d 1lt2 1re 2re
        cv c2 cr wf 1nn ffvelrn mp2an ltadd2i mpbi ruclem16 cseq1 c2nd ruclem14
        cop fveq2i c1st wa cmul cdiv cif ruclem9 ruclem5 ruclem11 oprex 3eqtr3i
        c3 op2nd 3brtr4i wi opreq1 fveq2d imbi12d elimel ruclem24 dedth nnind
        ax-mp ) DUCUDDHPZDIPZQRZOUAULZHPZWLIPZQRSHPZSIPZQRUBULZHPZWQIPZQRZWQSUE
        TZHPZXAIPZQRZWKUAUBDWLSUFWMWOWNWPQWLSHUGWLSIUGUHWLWQUFWMWRWNWSQWLWQHUGW
        LWQIUGUHWLXAUFWMXBWNXCQWLXAHUGWLXAIUGUHWLDUFWMWIWNWJQWLDHUGWLDIUGUHSGPZ
        SUETZXEUMUETZWOWPQSUMQRXFXGQRUISUMXEUJUKUCUNGUOSUCUDXEUNUDJUPUCUNSGUQUR
        USUTABCEFGHIJKLMNVASFEVBTPZVCPXFXGVEZVCPWPXGXHXIVCABCEFGHIJKLMNVDVFSEFI
        UPABCAULZVGPZBULZQRXLXJVCPZQRVHUMXLVITXMUETVQVJTXLUMXMVITZUETVQVJTVEUMX
        KVITXMUETVQVJTXKXNUETVQVJTVEVKFLVLEGJKVMNVNXFXGXESUEVOXEUMUEVOVRVPVSWQU
        CUDZWTXDVTXOWQSVKZHPZXPIPZQRZXPSUETZHPZXTIPZQRZVTWQSWQXPUFZWTXSXDYCYDWR
        XQWSXRQWQXPHUGWQXPIUGUHYDXBYAXCYBQYDXAXTHWQXPSUEWAZWBYDXAXTIYEWBUHWCABC
        XPEFGHIJKLMNWQSUCUPWDWEWFWGWH $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  Our constructed function ` G ` has an
         ever-increasing set of values. $)
      ruclem26 $p |- ( G ` A ) < ( G ` ( A + 1 ) ) $=
        ( cfv co clt wbr c3 c1 caddc wa c2 cmul cn cr wf peano2nn ax-mp ffvelrn
        cdiv mp2an ruclem23 ruclem1 ruclem22 2re remulcli readdcli 3re gt0ne0ii
        wcel 3pos redivcli lttri sylan2b ruclem18 breqtrrd wn ruclem25 ruclem20
        mpbi syl5breqr pm2.61i ) DHPZDUAUBQZGPZRSZVQDIPZRSZUCZVOVPHPZRSWAVOUDVQ
        UEQZVSUBQZTULQZWBRVTVRVQWERSVOWERSVQVSUFUGGUHVPUFVBZVQUGVBJDUFVBWFODUIU
        JUFUGVPGUKUMZABCDEFGHIJKLMNOUNZUOVOVQWEABCDEFGHIJKLMNOUPZWGWDTWCVSUDVQU
        QWGURWHUSUTTUTVCVAVDVEVFABCDEFGHIJKLMNOVGVHWAVIVOUDVOUEQVSUBQTULQZWBRVO
        VSRSVOWJRSABCDEFGHIJKLMNOVJVOVSWIWHUOVLABCDEFGHIJKLMNOVKVMVN $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  Our constructed function ` H ` has an
         ever-decreasing set of values. $)
      ruclem27 $p |- ( H ` ( A + 1 ) ) < ( H ` A ) $=
        ( cfv co clt wbr cn c1 caddc wa c2 cmul c3 cdiv ruclem19 cr wf peano2nn
        ax-mp ffvelrn mp2an ruclem23 ruclem3 biimpi adantl wn ruclem21 ruclem25
        wcel eqbrtrd ruclem22 mpbi syl6eqbr pm2.61i ) DHPZDUAUBQZGPZRSZVJDIPZRS
        ZUCZVIIPZVLRSVNVOVJUDVLUEQZUBQUFUGQZVLRABCDEFGHIJKLMNOUHVMVQVLRSZVKVMVR
        VJVLTUIGUJVITVBZVJUIVBJDTVBVSODUKULTUIVIGUMUNABCDEFGHIJKLMNOUOZUPUQURVC
        VNUSVOVHVPUBQUFUGQZVLRABCDEFGHIJKLMNOUTVHVLRSWAVLRSABCDEFGHIJKLMNOVAVHV
        LABCDEFGHIJKLMNOVDVTUPVEVFVG $.
        $( [15-Oct-2004] $)
    $}

    ${
      ruclem28.a $e |- A e. NN $.
      $( Lemma for ~ ruc .  A helper lemma for ~ ruclem29 . $)
      ruclem28 $p |- -. ( ( G ` ( A + 1 ) ) < ( F ` ( A + 1 ) ) /\
                      ( F ` ( A + 1 ) ) < ( H ` ( A + 1 ) ) ) $=
        ( cfv co clt wbr cn c1 caddc wa wn c2 cmul c3 cdiv cr wf peano2nn ax-mp
        ffvelrn mp2an ruclem23 ruclem1 biimpi adantl ruclem18 breqtrrd ruclem22
        wcel ltnsymi syl intnanrd ruclem26 lttri ruclem27 mpan2 anim12i pm2.61i
        mpan con3i ) DHPZDUAUBQZGPZRSZVPDIPZRSZUCZVOHPZVPRSZVPVOIPZRSZUCZUDVTWB
        WDVTVPWARSWBUDVTVPUEVPUFQVRUBQUGUHQZWARVSVPWFRSZVQVSWGVPVRTUIGUJVOTVBZV
        PUIVBJDTVBWHODUKULZTUIVOGUMUNZABCDEFGHIJKLMNOUOZUPUQURABCDEFGHIJKLMNOUS
        UTVPWAWJABCVOEFGHIJKLMNWIVAZVCVDVEWEVTWBVQWDVSVNWARSWBVQABCDEFGHIJKLMNO
        VFVNWAVPABCDEFGHIJKLMNOVAWLWJVGVLWDWCVRRSVSABCDEFGHIJKLMNOVHVPWCVRWJABC
        VOEFGHIJKLMNWIUOWKVGVIVJVMVK $.
        $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  At any index ` A ` , the interval between our
         constructed functions ` G ` and ` H ` does not include the
         corresponding value of input function ` F ` .  In other words, our
         constructed functions define, by ~ ruclem26 and ~ ruclem27 , an
         ever-shrinking interval that eventually squeezes out all values of
         ` F ` . $)
      ruclem29 $p |- -. ( ( G ` A ) < ( F ` A ) /\ ( F ` A ) < ( H ` A ) ) $=
        ( cfv clt wbr c1 fveq2 vw vv cn wcel wa wn cv caddc wceq breq12d notbid
        co anbi12d cr wf ffvelrn mp2an ltp1i ruclem16 breqtrri ruclem22 ltnsymi
        1nn ax-mp intnanr cif opreq1 fveq2d elimel ruclem28 dedth nn1suc ) DUCU
        DDHPZDGPZQRZVNDIPZQRZUEZUFZOUAUGZHPZVTGPZQRZWBVTIPZQRZUEZUFSHPZSGPZQRZW
        HSIPZQRZUEZUFUBUGZSUHULZHPZWNGPZQRZWPWNIPZQRZUEZUFZVSUAUBDVTSUIZWFWLXBW
        CWIWEWKXBWAWGWBWHQVTSHTVTSGTZUJXBWBWHWDWJQXCVTSITUJUMUKVTWNUIZWFWTXDWCW
        QWEWSXDWAWOWBWPQVTWNHTVTWNGTZUJXDWBWPWDWRQXEVTWNITUJUMUKVTDUIZWFVRXFWCV
        OWEVQXFWAVMWBVNQVTDHTVTDGTZUJXFWBVNWDVPQXGVTDITUJUMUKWIWKWHWGQRWIUFWHWH
        SUHULWGQWHUCUNGUOSUCUDWHUNUDJVCUCUNSGUPUQZURABCEFGHIJKLMNUSUTWHWGXHABCS
        EFGHIJKLMNVCVAVBVDVEWMUCUDZXAXIWMSVFZSUHULZHPZXKGPZQRZXMXKIPZQRZUEZUFWM
        SWMXJUIZWTXQXRWQXNWSXPXRWOXLWPXMQXRWNXKHWMXJSUHVGZVHXRWNXKGXSVHZUJXRWPX
        MWRXOQXTXRWNXKIXSVHUJUMUKABCXJEFGHIJKLMNWMSUCVCVIVJVKVLVD $.
        $( [15-Oct-2004] $)

      ${
        ruclem.b $e |- B e. NN $.
        $( Lemma for ~ ruc .  A helper lemma for ~ ruclem32 . $)
        ruclem30 $p |- ( ( G ` A ) < ( G ` ( A + B ) ) ->
                         ( G ` A ) < ( G ` ( A + ( B + 1 ) ) ) ) $=
          ( cfv cn wcel caddc co clt wbr c1 nnaddcl mp2an ruclem26 nncni ax-1cn
          addassi fveq2i breqtri ruclem22 peano2nn ax-mp lttri mpan2 ) DIRZDEUA
          UBZIRZUCUDVADEUEUAUBZUAUBZIRZUCUDUSVDUCUDVAUTUEUAUBZIRVDUCABCUTFGHIJK
          LMNODSTZESTZUTSTPQDEUFUGZUHVEVCIDEUEDPUIEQUIUJUKULUMUSVAVDABCDFGHIJKL
          MNOPUNABCUTFGHIJKLMNOVHUNABCVCFGHIJKLMNOVFVBSTZVCSTPVGVIQEUOUPDVBUFUG
          UNUQUR $.
          $( [15-Oct-2004] $)

        $( Lemma for ~ ruc .  A helper lemma for ~ ruclem32 . $)
        ruclem31 $p |- ( ( H ` ( A + B ) ) < ( H ` B ) ->
                         ( H ` ( ( A + 1 ) + B ) ) < ( H ` B ) ) $=
          ( caddc cn wcel c1 co cfv clt wbr nncni ax-1cn add32i fveq2i ruclem27
          nnaddcl mp2an eqbrtri peano2nn ax-mp ruclem23 lttri mpan ) DUARUBZERU
          BZJUCZDERUBZJUCZUDUEVCEJUCZUDUEVAVDUDUEVAVBUARUBZJUCVCUDUTVEJDUAEDPUF
          UGEQUFUHUIABCVBFGHIJKLMNODSTZESTZVBSTPQDEUKULZUJUMVAVCVDABCUTFGHIJKLM
          NOUSSTZVGUTSTVFVIPDUNUOQUSEUKULUPABCVBFGHIJKLMNOVHUPABCEFGHIJKLMNOQUP
          UQUR $.
          $( [15-Oct-2004] $)

        $( Lemma for ~ ruc .  All values of function ` G ` are less than all
           values of function ` H ` . $)
        ruclem32 $p |- ( G ` A ) < ( H ` B ) $=
          ( cfv caddc clt vw vv co wbr cn wcel cv opreq2 fveq2d breq2d ruclem26
          c1 wceq wi cif opreq1 opreq2d imbi12d 1nn elimel ruclem30 dedth nnind
          ax-mp nnaddcl ruclem25 ruclem22 ruclem23 breq1d ax-1cn addcomi fveq2i
          mp2an lttri nncni ruclem27 eqbrtri opreq1d ruclem31 ) DIRZDESUCZJRZTU
          DZWBEJRZTUDZVTWDTUDVTWAIRZTUDZWFWBTUDWCEUEUFZWGQVTDUAUGZSUCZIRZTUDVTD
          ULSUCZIRZTUDVTDUBUGZSUCZIRZTUDZVTDWNULSUCZSUCZIRZTUDZWGUAUBEWIULUMZWK
          WMVTTXBWJWLIWIULDSUHUIUJWIWNUMZWKWPVTTXCWJWOIWIWNDSUHUIUJWIWRUMZWKWTV
          TTXDWJWSIWIWRDSUHUIUJWIEUMZWKWFVTTXEWJWAIWIEDSUHUIUJABCDFGHIJKLMNOPUK
          WNUEUFZWQXAUNVTDXFWNULUOZSUCZIRZTUDZVTDXGULSUCZSUCZIRZTUDZUNWNULWNXGU
          MZWQXJXAXNXOWPXIVTTXOWOXHIWNXGDSUHUIUJXOWTXMVTTXOWSXLIXOWRXKDSWNXGULS
          UPZUQUIUJURABCDXGFGHIJKLMNOPWNULUEUSUTZVAVBVCVDABCWAFGHIJKLMNODUEUFZW
          HWAUEUFPQDEVEVMZVFVTWFWBABCDFGHIJKLMNOPVGZABCWAFGHIJKLMNOXSVGABCWAFGH
          IJKLMNOXSVHZVNVMXRWEPWIESUCZJRZWDTUDULESUCZJRZWDTUDWNESUCZJRZWDTUDZWR
          ESUCZJRZWDTUDZWEUAUBDXBYCYEWDTXBYBYDJWIULESUPUIVIXCYCYGWDTXCYBYFJWIWN
          ESUPUIVIXDYCYJWDTXDYBYIJWIWRESUPUIVIWIDUMZYCWBWDTYLYBWAJWIDESUPUIVIYE
          EULSUCZJRWDTYDYMJULEVJEQVOVKVLABCEFGHIJKLMNOQVPVQXFYHYKUNXGESUCZJRZWD
          TUDZXKESUCZJRZWDTUDZUNWNULXOYHYPYKYSXOYGYOWDTXOYFYNJWNXGESUPUIVIXOYJY
          RWDTXOYIYQJXOWRXKESXPVRUIVIURABCXGEFGHIJKLMNOXQQVSVBVCVDVTWBWDXTYAABC
          EFGHIJKLMNOQVHVNVM $.
          $( [15-Oct-2004] $)
      $}
    $}

    $( Lemma for ~ ruc .  The set of values of our constructed function ` G `
       is a non-empty subset of ` RR ` .  This is a helper lemma for theorems
       involving supremum. $)
    ruclem33 $p |- ( ran G C_ RR /\ ran G =/= (/) /\
                     E. w e. RR A. v e. ran G v <_ w ) $=
      ( cr cle cn c1 wcel crn wss c0 wne cv wbr wral wrex wf ruclem17 frn ax-mp
      cfv wfn ffn 1nn fnfvelrn mp2an ne0i ruclem23 wceq wb fvelrnb wa clt fveq2
      cif eleq1d elimel ruclem22 dedth jctir ruclem32 ltle sylc breq1 syl5ibcom
      breq1d rexlimiv sylbi rgen breq2 ralbidv rcla4ev 3pm3.2i ) IUAZPUBZWFUCUD
      ZEUEZDUEZQUFZEWFUGZDPUHZRPIUIZWGABCFGHIJKLMNOUJZRPIUKULSIUMZWFTZWHIRUNZSR
      TWQWNWRWORPIUOULZUPRSIUQURWFWPUSULSJUMZPTZWIWTQUFZEWFUGZWMABCSFGHIJKLMNOU
      PUTZXBEWFWIWFTZWJIUMZWIVAZDRUHZXBWRXEXHVBWSDRWIIVCULXGXBDRWJRTZXFWTQUFZXG
      XBXIXFPTZXAVDXFWTVEUFZXJXIXKXAXIXKXIWJSVGZIUMZPTWJSWJXMVAZXFXNPWJXMIVFZVH
      ABCXMFGHIJKLMNOWJSRUPVIZVJVKXDVLXIXLXNWTVEUFWJSXOXFXNWTVEXPVRABCXMSFGHIJK
      LMNOXQUPVMVKXFWTVNVOXFWIWTQVPVQVSVTWAWLXCDWTPWJWTVAWKXBEWFWJWTWIQWBWCWDUR
      WE $.
      $( [17-Jun-2007] $) $( [15-Oct-2004] $)

    ${
      $d w S $.
      ruclem.5 $e |- S = sup ( ran G , RR , < ) $.
      $( Lemma for ~ ruc .  The supremum of the set of values of our
         constructed function ` G ` is a real number. $)
      ruclem34 $p |- S e. RR $=
        ( vw vv crn cr clt csup ruclem33 suprclii eqeltri ) FHRZSTUASOPQUEABCPQ
        DEGHIJKLMNUBUCUD $.
        $( [15-Oct-2004] $)

      ${
        ruclem.a $e |- A e. NN $.
        $( Lemma for ~ ruc .  The supremum we have constructed lies between all
           values of the ` G ` and ` H ` functions.  Compare ~ ruclem29 , which
           states the opposite for the input function ` F ` . $)
        ruclem35 $p |- ( ( G ` A ) < S /\ S < ( H ` A ) ) $=
          ( clt wbr cn vw vv vu cfv c1 caddc co cle ruclem26 crn cr csup wfn wf
          ruclem17 ffn ax-mp peano2nn fnfvelrn mp2an ruclem33 suprubii breqtrri
          wcel ruclem22 ruclem34 ltletri wn cv wral ruclem23 wceq wb fvelrnb wa
          wrex cif fveq2 eleq1d elimel dedth jctir breq1d ruclem32 ltnsym breq2
          1nn sylc notbid syl5ibcom rexlimiv sylbi rgen c0 simp1i simp2i pm3.2i
          wss wne suprnubii breq2i mtbir lenlti mpbir ruclem27 lelttri ) DIUDZG
          RSZGDJUDZRSZXGDUEUFUGZIUDZRSXLGUHSXHABCDEFHIJKLMNOQUIXLIUJZUKRULZGUHX
          LXMVDZXLXNUHSITUMZXKTVDZXOTUKIUNXPABCEFHIJKLMNOUOTUKIUPUQZDTVDXQQDURU
          QZTXKIUSUTUAUBXMXLABCUAUBEFHIJKLMNOVAZVBUQPVCXGXLGABCDEFHIJKLMNOQVEAB
          CXKEFHIJKLMNOXSVEABCEFGHIJKLMNOPVFZVGUTGXKJUDZUHSZYBXIRSXJYCYBGRSZVHY
          DYBXNRSZYBUKVDZYBUCVIZRSZVHZUCXMVJYEVHABCXKEFHIJKLMNOXSVKZYIUCXMYGXMV
          DZUAVIZIUDZYGVLZUATVPZYIXPYKYOVMXRUATYGIVNUQYNYIUATYLTVDZYBYMRSZVHZYN
          YIYPYMUKVDZYFVOYMYBRSZYRYPYSYFYPYSYPYLUEVQZIUDZUKVDYLUEYLUUAVLZYMUUBU
          KYLUUAIVRZVSABCUUAEFHIJKLMNOYLUETWGVTZVEWAYJWBYPYTUUBYBRSYLUEUUCYMUUB
          YBRUUDWCABCUUAXKEFHIJKLMNOUUEXSWDWAYMYBWEWHYNYQYHYMYGYBRWFWIWJWKWLWMU
          CXMYBXMUKWRZXMWNWSZUUFUUGUBVIYLUHSUBXMVJUAUKVPZXTWOUUFUUGUUHXTWPWQWTU
          TGXNYBRPXAXBGYBYAYJXCXDABCDEFHIJKLMNOQXEGYBXIYAYJABCDEFHIJKLMNOQVKXFU
          TWQ $.
          $( [17-Oct-2004] $)

        $( Lemma for ~ ruc .  No value of ` F ` is equal to the supremum we
           have constructed. $)
        ruclem36 $p |- -. ( F ` A ) = S $=
          ( cfv clt wbr wceq ruclem29 ruclem35 breq2 breq1 anbi12d mpbiri mto
          wa ) DHRZGUAZDIRZUJSTZUJDJRZSTZUIZABCDEFHIJKLMNOQUBUKUPULGSTZGUNSTZUI
          ABCDEFGHIJKLMNOPQUCUKUMUQUOURUJGULSUDUJGUNSUEUFUGUH $.
          $( [15-Oct-2004] $)
      $}

      $( Lemma for ~ ruc .  If ` F ` is any function that maps ` NN ` into
         ` RR ` , then ` F ` cannot be onto ` RR ` . $)
      ruclem37 $p |- -. F : NN -onto-> RR $=
        ( vw cn cr wceq c1 wfo crn wcel cv cfv wrex cif fveq2 eqeq1d notbid 1nn
        wn elimel ruclem36 dedth nrex wfn wb wf ffn ax-mp fvelrnb ruclem34 forn
        mtbir syl5eleqr mto ) QRGUAZFGUBZUCZVJPUDZGUEZFSZPQUFZVMPQVKQUCZVMULVOV
        KTUGZGUEZFSZULVKTVKVPSZVMVRVSVLVQFVKVPGUHUIUJABCVPDEFGHIJKLMNOVKTQUKUMU
        NUOUPGQUQZVJVNURQRGUSVTJQRGUTVAPQFGVBVAVEVHFRVIABCDEFGHIJKLMNOVCQRGVDVF
        VG $.
        $( [13-Oct-2004] $)
    $}
  $}

  ${
    $d F z $.  $d x y z $.
    ruclem38.0 $e |- F : NN --> RR $.
    $( Lemma for ~ ruc .  If ` F ` is any function that maps ` NN ` into
       ` RR ` , then ` F ` cannot be onto ` RR ` .  Using ~ eqid , this lemma
       eliminates those hypotheses of ~ ruclem37 that are no longer needed. $)
    ruclem38 $p |- -. F : NN -onto-> RR $=
      ( vx vy vz c1 cfv caddc co c2 cop csn cv cr wa clt cmul c3 cdiv eqid cdif
      cn cres cun cxp wcel c1st wbr c2nd cif wceq copab2 ccom crn csup ruclem37
      cseq1 ) CDEFFAGZFHIURJHIKKLAUBFLUAUCUDZCMZNNUEUFDMZNUFOEMUTUGGZVAPUHVAUTU
      IGZPUHOJVAQIVCHIRSIVAJVCQIZHIRSIKJVBQIVCHIRSIVBVDHIRSIKUJUKOCDEULZUGVEUSU
      QIZUMZUNNPUOZAVGUIVFUMZBUSTVETVGTVITVHTUP $.
      $( [13-Oct-2004] $)
  $}

  $( Lemma for ~ ruc .  There is no function that maps ` NN ` onto ` RR ` .
     (Use ~ nex if you want this in the form
     ` -. E. f f : NN -onto-> RR ` .) $)
  ruclem39 $p |- -. F : NN -onto-> RR $=
    ( cn cr wfo wn wi wf fof cc0 csn cxp cif wceq foeq1 notbid wss 0re elisseti
    fconst ax-mp wcel snssi fss mp2an elimf ruclem38 dedth syl pm2.01 ) BCADZUJ
    EZFUKUJBCAGZUKBCAHULUKBCULABIJZKZLZDZEAUNAUOMUJUPBCAUONOUOBCAUNBUMUNGUMCPZB
    CUNGBIICQRSICUAUQQICUBTBUMCUNUCUDUEUFUGUHUJUIT $.
    $( [14-Oct-2004] $)

  $( The set of natural numbers is strictly dominated by the set of real
     numbers, i.e. the real numbers are uncountable.  The proof consists of
     lemmas ~ ruclem1 through ~ ruclem39 and this final piece.  Our proof is
     based on the proof of Theorem 5.18 of [Truss] p. 114.  See ~ ruclem39 for
     the function existence version of this theorem.  For an informal
     discussion of this proof, see
     ~ http://us.metamath.org/mpegif/mmcomplex.html#uncountable .  For an
     alternate proof see ~ rucALT . $)
  ruc $p |- NN ~< RR $=
    ( vf cn cr csdm wbr cdom cen wn brsdom cvv wcel wss reex nnssre ssdom2g mp2
    cv wf1o wex wfo ruclem39 f1ofo mto nex bren mtbir mpbir2an ) BCDEBCFEZBCGEZ
    HBCICJKBCLUHMNBCJOPUIBCAQZRZASUKAUKBCUJTUJUABCUJUBUCUDBCAMUEUFUG $.
    $( [13-Oct-2004] $)

  $( The set of rationals is strictly less equinumerous than the set of reals
     ( ` RR ` strictly dominates ` QQ ` ). $)
  resdomq $p |- QQ ~< RR $=
    ( cq cn cen wbr cr csdm qnnen ruc ensdomtr mp2an ) ABCDBEFDAEFDGHABEIJ $.
    $( [18-Dec-2004] $)

  $( There are at least aleph-one real numbers. $)
  aleph1re $p |- ( aleph ` 1o ) ~<_ RR $=
    ( c1o cale cfv cr cdom wbr csdm wn c0 csuc cen com aleph0 omex nnenom mp2an
    cn mpbir cvv wcel ensymi eqbrtri ruc ensdomtr wi wa alephnbtwn2 imnan ax-mp
    df-1o fveq2i breq2i mtbir wb fvex reex domtri ) ABCZDEFZDURGFZHZUTDIJZBCZGF
    ZIBCZDGFZVDHZVEQKFQDGFVFVELQKMQLNOUAUBUCVEQDUDPVFVGUEVFVDUFHIDUGVFVDUHRUIUR
    VCDGAVBBUJUKULUMURSTDSTUSVAUNABUOUPURDSSUQPR $.
    $( [2-Feb-2005] $)

  $( There are at least aleph-one irrationals. $)
  aleph1irr $p |- ( aleph ` 1o ) ~<_ ( RR \ QQ ) $=
    ( c1o cale cfv cr cdom wbr cq cdif cen aleph1re reex com csdm nnenom ensymi
    cn omex ruc ensdomtr mp2an sdomdom ax-mp resdomq qex infdif domentr ) ABCZD
    EFDDGHZIFUGUHEFJUHDKLDEFZGDMFUHDIFLDMFZUILPIFPDMFUJPLQNORLPDSTLDUAUBUCDGKUD
    UETOUGDUHUFT $.
    $( [2-Feb-2005] $)

