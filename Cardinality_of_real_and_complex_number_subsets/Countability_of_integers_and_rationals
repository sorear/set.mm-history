$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Countability of integers and rationals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u $.
    $( The cross product of the set of natural numbers with itself is
       equinumerous to the set of natural numbers.  (Revised by Mario Carneiro,
       9-Mar-2013.) $)
    xpnnen $p |- ( NN X. NN ) ~~ NN $=
      ( cn cxp com cen wbr nnenom omex xpen mp2an xpomen entr4i entri ) AABZCCB
      ZAACDEZOMNDEFFACACGGHINCAGJFKL $.
      $( [9-Mar-2013] $) $( [1-Aug-2004] $)
  $}

  ${
    $d x y z w v u $.
    $( The cross product of the set of natural numbers with itself is
       equinumerous to the set of natural numbers.  The key idea is to use
       ~ nn0opth2 to show that the mapping from natural numbers ` z ` and ` w `
       to ` ( ( z + w ) ^ 2 ) + w ` is one-to-one. $)
    xpnnenOLD $p |- ( NN X. NN ) ~~ NN $=
      ( vz vw vv vu cn wbr wcel nnex cv cint csn crn caddc co c2 cexp wa cn0 c1
      wceq vx cxp cdom cen cvv xpex cuni cop elxp5 nnaddcl 2nn0 nnexpcl sylancl
      vy sylancom adantl sylbi wrex wb elxp2 wi inteq inteqd visset op1stb sneq
      syl6eq rneqd unieqd op2nda opreq12d opreq1d nnnn0 anim12i nn0opth2 syl2an
      eqeqan12d sylan9bbr eqeq12 opth syl6bb bitr4d rexlimivv rexlimdvv syl2anb
      exp43 com23 imp dom2 ax-mp elisseti xpsnen ensymi ssid snssi xpss12 mp2an
      1nn wss ssdom2g mp2 endomtr sbth ) EEUBZEUCFZEXDUCFZXDEUDFXDUEGZXEEEHHUFZ
      UAUNXDEUAIZJZJZXIKZLZUGZMNZOPNZXNMNZUNIZJZJZXRKZLZUGZMNZOPNZYCMNZUEXIXDGZ
      XIXKXNUHTZXKEGZXNEGZQZQXQEGZXIEEUIYKYLYHYIYJXPEGZYLYKXOEGORGYMXKXNUJUKXOO
      ULUMXPXNUJUOUPUQYGXIAIZBIZUHZTZBEURAEURZXRCIZDIZUHZTZDEURCEURZXQYFTZXIXRT
      ZUSZXRXDGABXIEEUTCDXREEUTYRUUCUUFYRUUBUUFCDEEYQYSEGZYTEGZQZUUBUUFVAZVAABE
      EYNEGZYOEGZQZUUIYQUUJUUMUUIYQUUBUUFUUMUUIQZYQUUBQZQUUDYNYSTYOYTTQZUUEUUOU
      UDYNYOMNZOPNZYOMNZYSYTMNZOPNZYTMNZTZUUNUUPYQUUBXQUUSYFUVBYQXPUURXNYOMYQXO
      UUQOPYQXKYNXNYOMYQXKYPJZJYNYQXJUVDXIYPVBVCYNYOAVDZVEVGYQXNYPKZLZUGYOYQXMU
      VGYQXLUVFXIYPVFVHVIYNYOUVEBVDZVJVGZVKVLUVIVKUUBYEUVAYCYTMUUBYDUUTOPUUBXTY
      SYCYTMUUBXTUUAJZJYSUUBXSUVJXRUUAVBVCYSYTCVDZVEVGUUBYCUUAKZLZUGYTUUBYBUVMU
      UBYAUVLXRUUAVFVHVIYSYTUVKDVDZVJVGZVKVLUVOVKVQUUMYNRGZYORGZQYSRGZYTRGZQUVC
      UUPUSUUIUUKUVPUULUVQYNVMYOVMVNUUGUVRUUHUVSYSVMYTVMVNYNYOYSYTVOVPVRUUOUUEU
      UPUSUUNUUOUUEYPUUATUUPXIYPXRUUAVSYNYOYSYTUVEUVHUVNVTWAUPWBWFWGWCWDWHWEWIW
      JEESKZUBZUDFUWAXDUCFZXFUWAEHESHSEWRWKWLWMXGUWAXDWSZUWBXHEEWSUVTEWSZUWCEWN
      SEGUWDWRSEWOWJEEUVTEWPWQUWAXDUEWTXAEUWAXDXBWQXDEXCWQ $.
      $( [1-Aug-2004] $)
  $}

  $( The cross product of omega (the set of ordinal natural numbers) with
     itself is equinumerous to omega.  Exercise 1 of [Enderton] p. 133 (which
     proves this with a direct, but longer, proof; ours uses instead the
     Schroeder-Bernstein Theorem ~ sbth in ~ xpnnen ). $)
  xpomenOLD $p |- ( om X. om ) ~~ om $=
    ( com cxp cn omex xpex cen wbr nnenom xpen mp2an xpnnen entr3i entri ) AABZ
    CACCBZNCAADDECAFGZPONFGHHCACADDIJKLHM $.
    $( [23-Jul-2004] $)

  $( Lemma for ~ znnen . $)
  znnenlem $p |- ( ( ( 0 <_ x /\ -. 0 <_ y ) /\ ( x e. ZZ /\ y e. ZZ ) ) ->
                  ( x = y <-> ( 2 x. x ) = ( ( -u 2 x. y ) + 1 ) ) ) $=
    ( cc0 cv cle wbr wa cz wcel c2 cmul co cneg c1 caddc wne cr wi zre clt ltne
    wn wb 0re ltnle adantr anbi1d ltletr mp3an2 sylbird ancomsd 3impia syld3an3
    3com12 3expia syl2an impcom znegcl zneo sylan2 wceq cc 2cn mulneg12 sylancr
    mpan2 zcn adantl opreq1d neeqtrrd 2thd necon4bid ) CADZEFZCBDZEFUBZGZVMHIZV
    OHIZGZGZVMVOJVMKLZJMVOKLZNOLZWAVMVOPZWBWDPZVTVQWEVRVMQIZVOQIZVQWERVSVMSVOSW
    GWHVQWEWHWGVQWEWHWGVQVOVMTFZWEWHWGVQWIWHWGGZVPVNWIWJVPVNGVOCTFZVNGZWIWJWKVP
    VNWHWKVPUCZWGWHCQIZWMUDVOCUEVFUFUGWHWNWGWLWIRUDVOCVMUHUIUJUKULVOVMUAUMUNUOU
    PUQVTWFVQVTWBJVOMZKLZNOLZWDVSVRWOHIWBWQPVOURVMWOUSUTVTWCWPNOVSWCWPVAZVRVSJV
    BIVOVBIWRVCVOVGJVOVDVEVHVIVJVHVKVL $.
    $( [11-Jan-2008] $) $( [31-Jul-2004] $)

  ${
    $d x y z $.
    $( The set of integers and the set of natural numbers are equinumerous.
       Exercise 1 of [Gleason] p. 140.  (The proof was shortened by Mario
       Carneiro, 16-Jun-2013.) $)
    znnen $p |- ZZ ~~ NN $=
      ( vx vz vy cz cn wbr cen cn0 con0 c1st cfv c2nd cmin co com wcel wceq cc0
      mp2an fveq2 cdom cxp cv wrex cmpt omelon omex nn0ennn nnenom entr2i breq1
      wfo rcla4ev xpnum wf wral dffo3 0nn0 wi xp1st nn0z zsubcl syl2an syl11anc
      xp2nd a1i eqid fmptd ax-mp wo cr elznn simprbi nnnn0 cop opelxpi mpan2 cc
      cneg nn0cn subid1 syl eqcomd visset op1st syl6eq elisseti opreq12d eqeq2d
      0cn op2nd mpan zcn caddc subneg addid2 eqtr2d negex expcom jaod mpd oprex
      weq fvmpt rexbiia sylibr rgen mpbir2an nn0ex xpex fodomnum mp2 entri xpen
      xpomen entr4i domentr cvv wss nnex nnssz ssdomg sbth ) DEUAFZEDUAFZDEGFDH
      HUBZUAFZYFEGFYDAUCZYFGFAIUDZYFDAYFYHJKZYHLKZMNZUEZULZYGYHHGFZAIUDZYPYIOIP
      OHGFZYPUFHEOUGUHUIUJYOYQAOIYHOHGUKUMSZYRAHHUNSYNYFDYMUOZBUCZCUCZYMKZQZCYF
      UDZBDUPCBYFDYMUQRHPZYSURUUEAYFYLDYMYHYFPZYLDPZUSUUEUUFYJHPZYKHPZUUGYHHHUT
      YHHHVEUUHYJDPYKDPUUGUUIYJVAYKVAYJYKVBVCVDVFYMVGZVHVIUUDBDYTDPZYTUUAJKZUUA
      LKZMNZQZCYFUDZUUDUUKYTEPZYTVSZHPZVJZUUPUUKYTVKPUUTYTVLVMUUKUUQUUPUUSUUQUU
      PUSUUKUUQYTHPZUUPYTVNUVAYTRVOZYFPZYTYTRMNZQZUUPUVAUUEUVCURYTRHHVPVQUVAUVD
      YTUVAYTVRPZUVDYTQYTVTYTWAWBWCUUOUVECUVBYFUUAUVBQZUUNUVDYTUVGUULYTUUMRMUVG
      UULUVBJKYTUUAUVBJTYTRBWDZWEWFUVGUUMUVBLKRUUAUVBLTYTRUVHRVRWJWGZWKWFWHWIUM
      VDWBVFUUSUUKUUPUUSRUURVOZYFPZYTRUURMNZQZUUPUUKUUEUUSUVKURRUURHHVPWLUUKUVF
      UVMYTWMUVFUVLRYTWNNZYTRVRPUVFUVLUVNQWJRYTWOWLYTWPWQWBUUOUVMCUVJYFUUAUVJQZ
      UUNUVLYTUVOUULRUUMUURMUVOUULUVJJKRUUAUVJJTRUURUVIWEWFUVOUUMUVJLKUURUUAUVJ
      LTRUURUVIYTWRWKWFWHWIUMVCWSWTXAUUCUUOCYFUUAYFPUUBUUNYTAUUAYLUUNYFYMACXCYJ
      UULYKUUMMYHUUAJTYHUUALTWHUUJUULUUMMXBXDWIXEXFXGXHAYFDYMHHXIXIXJXKXLYFOEUG
      YFOOUBZOHOGFZUVQYFUVPGFHEOUHUIXMZUVRHOHOUGUGXNSXOXMUIXPDYFEXQSEXRPEDXSYEX
      TYAEDXRYBXLDEYCS $.
      $( [16-Jun-2013] $) $( [31-Jul-2004] $)
  $}

  ${
    $d w x y z $.  $d q r $.  $d a x y $.  $d b x y $.
    $( The rational numbers are countable.  This proof does not use the Axiom
       of Choice, even though it uses an onto function, because the base set
       ` ( ZZ X. NN ) ` is numerable.  Exercise 2 of [Enderton] p. 133.
       (Revised by Mario Carneiro, 3-Mar-2013.) $)
    qnnen $p |- QQ ~~ NN $=
      ( vq vx vy vz vw cq cn cdom wbr cen cz cxp con0 wrex wcel wceq mp2an nnex
      cv com wa cdiv co copab2 wfo omelon omex znnen nnenom entr2i breq1 ensymi
      rcla4ev xpnum wfn crn df-fo oprex eqid fnoprab2 elrnoprab bitr4i mpbir2an
      elq eqriv zex xpex fodomnum mp2 enref xpen xpnnen entri domentr cvv nnssq
      wss ssdomg sbth ) FGHIZGFHIZFGJIFKGLZHIZWBGJIVTASZWBJIAMNZWBFBSZKOCSZGOUA
      DSWFWGUBUCZPUABCDUDZUEZWCWDKJIZAMNZWDGJIZAMNZWETMOZTKJIZWLUFKGTUGUHUIUJWK
      WPATMWDTKJUKUMQWOTGJIZWNUFGTUGUIULWMWQATMWDTGJUKUMQAKGUNQWJWIWBUOWIUPZFPW
      BFWIUQBCDKGWHWIWFWGUBURZWIUSZUTEWRFESZWROXAWHPCGNBKNXAFOBCDKGWHXAWIWSWTVA
      BCXAVDVBVEVCAWBFWIKGVFRVGVHVIWBGGLZGKGJIGGJIWBXBJIUHGRVJKGGGRRVKQVLVMFWBG
      VNQGVOOGFVQWARVPGFVOVRVIFGVSQ $.
      $( [3-Mar-2013] $) $( [31-Jul-2004] $)
  $}


