$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
     Star rings through Hilbert spaces using extensible structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( $c C_rng $. $)
  $( Extend class notation subring. $)
  $( csrng $a class C_rng $. $)

  ${
    $d r s x y $.
    $( Def. 8.5.8, p. 216. $)
    $( Define ` s ` is a subring of ` r ` . $)
    $(
    df-subring $a |- C_rng = { <. s , r >. | ( ( Base ` s ) C_ ( Base ` r )
        /\ A. x e. ( Base ` s ) A. y e. ( Base ` s ) ( ( x ( +g ` s ) y )
  = ( x ( +g ` r ) y ) /\ ( x ( .r ` s ) y ) = ( x ( .r ` r ) y ) ) ) } $.
    $)
  $}

  $c *v $.
  $c Ring* $.

  $( Extend class notation with involution. $)
  cstv $a class *v $.

  $( Extend class notation with class of all *-rings. $)
  csr $a class Ring* $.

  $( Def. 8.5.23, p. 225. $)
  $( Define involution (the star operation of a *-ring). $)
  df-starv $a |- *v = ( v e. _V |-> ( v ` 4 ) ) $.

  $( Utility theorem: index-independent form of ~ df-starv . $)
  starvid $p |- *v = ( f e. _V |-> ( f ` ( *v ` ndx ) ) ) $=
    ( cstv c4 df-starv 4nn ndxid ) BCAADEF $.
    $( [6-Oct-1013] $)

  ${
    $d f i k p q r t $.
    $( Def. 8.5.6 p. 216 $)
    $( Define class of all star rings.  A star ring is a ring with an
       involution operation. $)
    df-starrng $a |- Ring* = { f e. Ring |
       E. k E. p E. t E. i ( ( k = ( Base ` f ) /\ p = ( +g ` f ) )
          /\ ( t = ( .r ` f ) /\ i = ( *v ` f ) ) /\
         A. q e. k A. r e. k ( ( i ` q ) e. k
              /\ ( ( i ` ( q p r ) ) = ( ( i ` q ) p ( i ` r ) )
              /\ ( i ` ( q t r ) ) = ( ( i ` r ) t ( i ` q ) )
              /\ ( i ` ( i ` q ) ) = q ) ) ) } $.
  $}

  ${
    $d f i k p q r t I $.  $d f i k p q r t K $.  $d f i k p q r t P $.
    $d f i k p q r t R $.  $d f i k p q r t T $.
    issrng.k $e |- K = ( Base ` R ) $.
    issrng.p $e |- P = ( +g ` R ) $.
    issrng.t $e |- T = ( .r ` R ) $.
    issrng.i $e |- I = ( *v ` R ) $.
    $( The predicate "is a star ring." $)
    issrng $p |- ( R e. Ring* <-> ( R e. Ring
        /\ A. q e. K A. r e. K ( ( I ` q ) e. K
              /\ ( ( I ` ( q P r ) ) = ( ( I ` q ) P ( I ` r ) )
              /\ ( I ` ( q T r ) ) = ( ( I ` r ) T ( I ` q ) )
              /\ ( I ` ( I ` q ) ) = q ) ) ) ) $=
      ( vk vp vt vi vf csr wcel crg cv wceq wa cfv co w3a wral wex cbs cplusg
      cmulr cstv fveq2 syl6eqr eqeq2d anbi12d 3anbi12d 4exbidv df-starrng
      elrab2 cvv fvex eqeltri eleq2 anbi1d raleqbi1dv opreq fveq2d eqeq12d
      3anbi1d anbi2d 2ralbidv 3anbi2d fveq1 eleq1d opreq12d id fveq12d eqeq1d
      3anbi123d ceqsex4v anbi2i bitri ) BQRBSRZLTZEUAZMTZAUAZUBZNTZCUAZOTZDUAZU
      BZGTZWKUCZWDRZWNFTZWFUDZWKUCZWOWQWKUCZWFUDZUAZWNWQWIUDZWKUCZWTWOWIUDZUAZW
      OWKUCZWNUAZUEZUBZFWDUFZGWDUFZUEZOUGNUGMUGLUGZUBWCWNDUCZERZWNWQAUDZDUCZXOW
      QDUCZAUDZUAZWNWQCUDZDUCZXSXOCUDZUAZXODUCZWNUAZUEZUBZFEUFGEUFZUBWDPTZUHUCZ
      UAZWFYKUIUCZUAZUBZWIYKUJUCZUAZWKYKUKUCZUAZUBZXLUEZOUGNUGMUGLUGXNPBSQYKBUA
      ZUUBXMLMNOUUCYPWHUUAWMXLUUCYMWEYOWGUUCYLEWDUUCYLBUHUCZEYKBUHULHUMUNUUCYNA
      WFUUCYNBUIUCZAYKBUIULIUMUNUOUUCYRWJYTWLUUCYQCWIUUCYQBUJUCZCYKBUJULJUMUNUU
      CYSDWKUUCYSBUKUCZDYKBUKULKUMUNUOUPUQNPOLFGMURUSXNYJWCXLWOERZXIUBZFEUFZGEU
      FUUHXQWKUCZWOWTAUDZUAZXFXHUEZUBZFEUFGEUFUUHUUMYBWKUCZWTWOCUDZUAZXHUEZUBZF
      EUFGEUFYJLMNOEACDEUUDUTHBUHVAVBAUUEUTIBUIVAVBCUUFUTJBUJVAVBDUUGUTKBUKVAVB
      XKUUJGWDEXJUUIFWDEWEWPUUHXIWDEWOVCVDVEVEWGUUIUUOGFEEWGXIUUNUUHWGXBUUMXFXH
      WGWSUUKXAUULWGWRXQWKWNWQWFAVFVGWOWTWFAVFVHVIVJVKWJUUOUUTGFEEWJUUNUUSUUHWJ
      XFUURUUMXHWJXDUUPXEUUQWJXCYBWKWNWQWICVFVGWTWOWICVFVHVLVJVKWLUUTYIGFEEWLUU
      HXPUUSYHWLWOXOEWNWKDVMZVNWLUUMYAUURYEXHYGWLUUKXRUULXTXQWKDVMWLWOXOWTXSAUV
      AWQWKDVMZVOVHWLUUPYCUUQYDYBWKDVMWLWTXSWOXOCUVBUVAVOVHWLXGYFWNWLWOXOWKDWLV
      PUVAVQVRVSUOVKVTWAWB $.
      $( [22-Sep-2011] $)
  $}

  $c DivRing* $.

  $( Extend class notation with class of all *-division rings. $)
  csd $a class DivRing* $.

  $( Define the class notation with class of all *-division rings, which are
     division rings that are also *-rings. $)
  df-sdivrng $a |- DivRing* = ( DivRing i^i Ring* ) $.

  $( The predicate "is a *-division ring." $)
  issdrng $p |- ( R e. DivRing* <-> ( R e. DivRing /\ R e. Ring* ) ) $=
    ( csd wcel cdivring csr cin wa df-sdivrng eleq2i elin bitri ) ABCADEFZCADCA
    ECGBLAHIADEJK $.
    $( [22-Sep-2011] $)

  $c ip $.
  $c PreHil $.

  $( Extend class notation with Hermitian form (inner product). $)
  cipr $a class ip $.

  $( Extend class notation with class all pre-Hilbert spaces. $)
  cprehil $a class PreHil $.

  $( Def. 8.5.25, p. 226. $)
  $( Define Hermitian form (inner product). $)
  df-inprod $a |- ip = ( v e. _V |-> ( v ` 8 ) ) $.

  $( Utility theorem: index-independent form of ~ df-ip . $)
  ipid $p |- ip = ( f e. _V |-> ( f ` ( ip ` ndx ) ) ) $=
    ( cipr c8 df-inprod 8nn ndxid ) BCAADEF $.
    $( [6-Oct-1013] $)

  $( Utility lemma for constructing a pre-Hilbert space. $)
  phllem1 $p |- -. ( *v ` ndx ) e. ( { ( Base ` ndx ) , ( +g ` ndx ) ,
   ( .r ` ndx ) } u. { ( vbase ` ndx ) , ( vadd ` ndx ) , ( vsca ` ndx ) } ) $=
    ( vs cnx cstv cfv cbs cplusg cmulr ctp cvbase cvadd cvsca cun wcel wceq
    w3o wo wne w3a wn c4 df-starv 4nn ndxarg c1 1re 4re 1lt4 ltneii basendx
    neeqtrri eqnetri c2 2re 2lt4 df-plusg 2nn c3 3re 3lt4 df-mulr 3nn 3pm3.2i
    ne3anior mpbi c5 df-vbase 5nn 5re 4lt5 necomi c6 df-vadd 6nn 6re 4lt6 c7
    df-vsca 7nn 7re 4lt7 pm3.2ni elun fvex eltp orbi12i bitri mtbir ) BCDZBEDZB
    FDZBGDZHZBIDZBJDZBKDZHZLMZWHWINWHWJNWHWKNOZWHWMNWHWNNWHWONOZPZWRWSWHWIQZWHW
    JQZWHWKQZRWRSXAXBXCWHTWICTAAUAUBUCZTUDWIUDTUEUFUGUHUIUJUKWHTWJXDTULWJULTUMU
    FUNUHFULAAUOUPUCUJUKWHTWKXDTUQWKUQTURUFUSUHGUQAAUTVAUCUJUKVBWHWIWHWJWHWKVCV
    DWHWMQZWHWNQZWHWOQZRWSSXEXFXGWMWHWMVEWHIVEAAVFVGUCVETWHTVEUFVHVIUHXDUJUKVJW
    NWHWNVKWHJVKAAVLVMUCVKTWHTVKUFVNVOUHXDUJUKVJWOWHWOVPWHKVPAAVQVRUCVPTWHTVPUF
    VSVTUHXDUJUKVJVBWHWMWHWNWHWOVCVDWAWQWHWLMZWHWPMZPWTWHWLWPWBXHWRXIWSWHWIWJWK
    BCWCZWDWHWMWNWOXJWDWEWFWG $.
    $( [30-Sep-1013] $)

  $( Utility lemma for constructing a pre-Hilbert space. $)
  phllem2 $p |- -. ( ip ` ndx ) e. ( ( { ( Base ` ndx ) , ( +g ` ndx ) ,
   ( .r ` ndx ) } u. { ( vbase ` ndx ) , ( vadd ` ndx ) , ( vsca ` ndx ) } )
        u. { ( *v ` ndx ) } ) $=
    ( vs cnx cipr cfv cbs cplusg cmulr ctp cvbase cvadd cvsca cun cstv csn
    wcel wceq w3o wo wne w3a wn c8 df-inprod 8nn ndxarg c1 1re 8re 1lt8 ltneii
    basendx neeqtrri eqnetri c2 2re 2lt8 df-plusg 2nn c3 3re 3lt8 df-mulr 3nn
    3pm3.2i ne3anior mpbi c5 5re 5lt8 df-vbase 5nn c6 6re 6lt8 df-vadd 6nn c7
    7re 7lt8 df-vsca 7nn pm3.2ni c4 4re 4lt8 df-starv 4nn df-ne elun fvex eltp
    orbi12i bitri elsnc mtbir ) BCDZBEDZBFDZBGDZHZBIDZBJDZBKDZHZLZBMDZNZLOZWPWQ
    PWPWRPWPWSPQZWPXAPWPXBPWPXCPQZRZWPXFPZRZXKXLXIXJWPWQSZWPWRSZWPWSSZTXIUAXNXO
    XPWPUBWQCUBAAUCUDUEZUBUFWQUFUBUGUHUIUJUKULUMWPUBWRXQUBUNWRUNUBUOUHUPUJFUNAA
    UQURUEULUMWPUBWSXQUBUSWSUSUBUTUHVAUJGUSAAVBVCUEULUMVDWPWQWPWRWPWSVEVFWPXASZ
    WPXBSZWPXCSZTXJUAXRXSXTWPUBXAXQUBVGXAVGUBVHUHVIUJIVGAAVJVKUEULUMWPUBXBXQUBV
    LXBVLUBVMUHVNUJJVLAAVOVPUEULUMWPUBXCXQUBVQXCVQUBVRUHVSUJKVQAAVTWAUEULUMVDWP
    XAWPXBWPXCVEVFWBWPXFSXLUAWPUBXFXQUBWCXFWCUBWDUHWEUJMWCAAWFWGUEULUMWPXFWHVFW
    BXHWPXEOZWPXGOZRXMWPXEXGWIYAXKYBXLYAWPWTOZWPXDOZRXKWPWTXDWIYCXIYDXJWPWQWRWS
    BCWJZWKWPXAXBXCYEWKWLWMWPXFYEWNWLWMWO $.
    $( [30-Sep-1013] $)

  ${
    phlfn.h $e |- H = ( ( { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. ,
                   <. ( .r ` ndx ) , T >. }
                u. { <. ( vbase ` ndx ) , V >. , <. ( vadd ` ndx ) , A >. ,
                   <. ( vsca ` ndx ) , S >. } )
             u. { <. ( *v ` ndx ) , R >. , <. ( ip ` ndx ) , I >. } ) $.
    ${
      phlfn.d $e |- D = ( ( { ( Base ` ndx ) , ( +g ` ndx ) , ( .r ` ndx ) }
                  u. { ( vbase ` ndx ) , ( vadd ` ndx ) , ( vsca ` ndx ) } )
               u. { ( *v ` ndx ) , ( ip ` ndx ) } ) $.
      $( A constructed pre-Hilbert space is a function.  Starting from ~ lmodfn
         (which has 6 members), we chain ~ fnunsn twice, each time adding an
         ordered pair to the function, to get all 8 members. $)
      phlfn $p |- H Fn D $=
        ( cnx cbs cfv cplusg cmulr ctp cvbase cvadd cvsca cun cstv csn cop
        cipr fvex eqid lmodfn phllem1 fnunsn cpr df-pr uneq2i unass 3eqtr4i
        phllem2 ) MNOZMPOZMQOZRMSOZMTOZMUAOZRUBZMUCOZUDZUBZCURBUEUSDUEUTGUERVAJ
        UEVBAUEVCFUERUBZVEEUEZUDZUBZHMUFOZIMUFUGVDVGVHVKVEEMUCUGABVDDFGJVHVHUHV
        DUHUIVKUHVGUHUJUKVHVIVLIUEZULZUBVHVJVMUDZUBZUBHVKVOUBVNVPVHVIVMUMUNKVHV
        JVOUOUPVDVEVLULZUBVDVFVLUDZUBZUBCVGVRUBVQVSVDVEVLUMUNLVDVFVRUOUPUQUK $.
        $( [1-Oct-1013] $)
    $}

    $( Lemma to shorten proofs of ~ phlbase through ~ phlip . $)
    phlfun $p |- Fun H $=
      ( cnx cbs cfv cplusg cmulr ctp cvbase cvadd cvsca cun cstv cipr cpr wfn
      wfun eqid phlfn fnfun ax-mp ) GKLMKNMKOMPKQMKRMKSMPTKUAMKUBMUCTZUDGUEABUJ
      CDEFGHIJUJUFUGUJGUHUI $.
      $( [6-Oct-1013] $)

    $( The base set of a constructed pre-Hilbert space. $)
    phlbase $p |- ( B e. X -> B = ( Base ` H ) ) $=
      ( vs cbs cnx cfv cop cplusg cmulr ctp cvbase cvadd cvsca cun cstv cipr
      cpr cvv tpex unex prex eqeltri phlfun baseid ssun1 sstri opex tpid1
      sselii eleqtrri strfv ) BGMJLGNMOZBPZNQOCPZNROFPZSZNTOIPZNUAOAPZNUBOEPZSZ
      UCZNUDODPZNUEOHPZUFZUCZUGKVJVMVEVIVBVCVDUHVFVGVHUHUIVKVLUJUIUKABCDEFGHIKU
      LLUMVBVNGVEVNVBVEVJVNVEVIUNVJVMUNUOVBVCVDVABUPUQURKUSUT $.
      $( [6-Oct-1013] $)

    $( The additive operation of a constructed pre-Hilbert space. $)
    phlplusg $p |- ( P e. X -> P = ( +g ` H ) ) $=
      ( vs cplusg cnx cbs cfv cop cmulr ctp cvbase cvadd cvsca cun cstv cipr
      cpr cvv tpex unex prex eqeltri phlfun plusgid ssun1 sstri opex tpid2
      sselii eleqtrri strfv ) CGMJLGNOPBQZNMPZCQZNRPFQZSZNTPIQZNUAPAQZNUBPEQZSZ
      UCZNUDPDQZNUEPHQZUFZUCZUGKVJVMVEVIVAVCVDUHVFVGVHUHUIVKVLUJUIUKABCDEFGHIKU
      LLUMVCVNGVEVNVCVEVJVNVEVIUNVJVMUNUOVAVCVDVBCUPUQURKUSUT $.
      $( [6-Oct-1013] $)

    $( The multiplicative operation of a constructed pre-Hilbert space. $)
    phlmulr $p |- ( T e. X -> T = ( .r ` H ) ) $=
      ( vs cmulr cnx cbs cfv cop cplusg ctp cvbase cvadd cvsca cun cstv cipr
      cpr cvv tpex unex prex eqeltri phlfun mulrid ssun1 sstri opex tpid3
      sselii eleqtrri strfv ) FGMJLGNOPBQZNRPCQZNMPZFQZSZNTPIQZNUAPAQZNUBPEQZSZ
      UCZNUDPDQZNUEPHQZUFZUCZUGKVJVMVEVIVAVBVDUHVFVGVHUHUIVKVLUJUIUKABCDEFGHIKU
      LLUMVDVNGVEVNVDVEVJVNVEVIUNVJVMUNUOVAVBVDVCFUPUQURKUSUT $.
      $( [6-Oct-1013] $)

    $( The set of vectors in a constructed pre-Hilbert space. $)
    phlvbase $p |- ( V e. X -> V = ( vbase ` H ) ) $=
      ( vs cvbase cnx cbs cfv cop cplusg cmulr ctp cvadd cvsca cun cstv cipr
      cpr cvv tpex unex prex eqeltri phlfun vbaseid ssun2 ssun1 sstri opex
      tpid1 sselii eleqtrri strfv ) IGMJLGNOPBQZNRPCQZNSPFQZTZNMPZIQZNUAPAQZNUB
      PEQZTZUCZNUDPDQZNUEPHQZUFZUCZUGKVKVNVEVJVBVCVDUHVGVHVIUHUIVLVMUJUIUKABCDE
      FGHIKULLUMVGVOGVJVOVGVJVKVOVJVEUNVKVNUOUPVGVHVIVFIUQURUSKUTVA $.
      $( [6-Oct-1013] $)

    $( The vector addition operation of a constructed pre-Hilbert space. $)
    phlvadd $p |- ( A e. X -> A = ( vadd ` H ) ) $=
      ( vs cvadd cnx cbs cfv cop cplusg cmulr ctp cvbase cvsca cun cstv cipr
      cpr cvv tpex unex prex eqeltri phlfun vaddid ssun2 ssun1 sstri opex
      tpid2 sselii eleqtrri strfv ) AGMJLGNOPBQZNRPCQZNSPFQZTZNUAPIQZNMPZAQZNUB
      PEQZTZUCZNUDPDQZNUEPHQZUFZUCZUGKVKVNVEVJVBVCVDUHVFVHVIUHUIVLVMUJUIUKABCDE
      FGHIKULLUMVHVOGVJVOVHVJVKVOVJVEUNVKVNUOUPVFVHVIVGAUQURUSKUTVA $.
      $( [6-Oct-1013] $)

    $( The scalar product operation of a constructed pre-Hilbert space. $)
    phlvsca $p |- ( S e. X -> S = ( vsca ` H ) ) $=
      ( vs cvsca cnx cbs cfv cop cplusg cmulr ctp cvbase cvadd cun cstv cipr
      cpr cvv tpex unex prex eqeltri phlfun vscaid ssun2 ssun1 sstri opex
      tpid3 sselii eleqtrri strfv ) EGMJLGNOPBQZNRPCQZNSPFQZTZNUAPIQZNUBPAQZNMP
      ZEQZTZUCZNUDPDQZNUEPHQZUFZUCZUGKVKVNVEVJVBVCVDUHVFVGVIUHUIVLVMUJUIUKABCDE
      FGHIKULLUMVIVOGVJVOVIVJVKVOVJVEUNVKVNUOUPVFVGVIVHEUQURUSKUTVA $.
      $( [6-Oct-1013] $)

    $( The involution operation of a constructed pre-Hilbert space. $)
    phlstarv $p |- ( R e. X -> R = ( *v ` H ) ) $=
      ( vs cstv cnx cbs cfv cop cplusg cmulr ctp cvbase cvadd cvsca cun cipr
      cpr cvv tpex unex prex eqeltri phlfun starvid ssun2 opex prid1 sselii
      eleqtrri strfv ) DGMJLGNOPBQZNRPCQZNSPFQZTZNUAPIQZNUBPAQZNUCPEQZTZUDZNMPZ
      DQZNUEPHQZUFZUDZUGKVHVLVCVGUTVAVBUHVDVEVFUHUIVJVKUJUIUKABCDEFGHIKULLUMVJV
      MGVLVMVJVLVHUNVJVKVIDUOUPUQKURUS $.
      $( [6-Oct-1013] $)

    $( The inner product (Hermitian form) operation of a constructed
       pre-Hilbert space. $)
    phlip $p |- ( I e. X -> I = ( ip ` H ) ) $=
      ( vs cipr cnx cbs cfv cop cplusg cmulr ctp cvbase cvadd cvsca cun cstv
      cpr cvv tpex unex prex eqeltri phlfun ipid ssun2 opex prid2 sselii
      eleqtrri strfv ) HGMJLGNOPBQZNRPCQZNSPFQZTZNUAPIQZNUBPAQZNUCPEQZTZUDZNUEP
      DQZNMPZHQZUFZUDZUGKVHVLVCVGUTVAVBUHVDVEVFUHUIVIVKUJUIUKABCDEFGHIKULLUMVKV
      MGVLVMVKVLVHUNVIVKVJHUOUPUQKURUS $.
      $( [6-Oct-1013] $)
  $}

  ${
    $d a f h i k p q r s t v w x y z $.
    $( Def. 8.5.24, p. 225. $)
    $( Define class all generalized pre-Hilbert spaces (over *-division
       ring). $)
    df-prehil $a |- PreHil = { f e. LVec |
       E. k E. p E. t E. i E. v E. a E. s E. h (
        ( ( k = ( Base ` f ) /\ p = ( +g ` f ) )
              /\ ( t = ( .r ` f ) /\ i = ( *v ` f ) ) )
        /\ ( ( v = ( vbase ` f ) /\ a = ( vadd ` f ) )
              /\ ( s = ( vsca ` f ) /\ h = ( ip ` f ) ) )
          /\ A. q e. k A. x e. v A. y e. v A. z e. v (
            ( x h y ) e. k
              /\ ( ( ( ( q s x ) a y ) h z )
                  = ( ( q t ( x h z ) ) p ( y h z ) )
        /\ ( ( x h x ) = ( 0g ` f ) -> x = ( 0v ` f ) )
              /\ ( i ` ( x h y ) ) = ( y h x ) ) ) ) } $.
  $}

  ${
    $d a f h i k p q s t v x y z A $.  $d a f h i k p q s t v x y z H $.
    $d a f h i k p q s t v x y z I $.  $d a f h i k p q s t v x y z K $.
    $d a f h i k p s t v N $.  $d a f h i k p q s t v x y z P $.
    $d a f h i k p q s t v x y z S $.  $d a f h i k p q s t v x y z T $.
    $d a f h i k p q s t v x y z V $.  $d a f h i k p q s t v x y z W $.
    $d a f h i k p s t v Z $.
    isphil.k $e |- K = ( Base ` W ) $.
    isphil.p $e |- P = ( +g ` W ) $.
    isphil.t $e |- T = ( .r ` W ) $.
    isphil.i $e |- I = ( *v ` W ) $.
    isphil.v $e |- V = ( vbase ` W ) $.
    isphil.a $e |- A = ( vadd ` W ) $.
    isphil.s $e |- S = ( vsca ` W ) $.
    isphil.h $e |- H = ( ip ` W ) $.
    isphil.n $e |- N = ( 0g ` W ) $.
    isphil.z $e |- Z = ( 0v ` W ) $.
    $( The predicate "is a pre-Hilbert space" (over a *-division ring) $)
    isphil $p |- ( W e. PreHil <-> ( W e. LVec
          /\ A. q e. K A. x e. V A. y e. V A. z e. V (
             ( x H y ) e. K
              /\ ( ( ( ( q S x ) A y ) H z )
                  = ( ( q T ( x H z ) ) P ( y H z ) )
         /\ ( ( x H x ) = N -> x = Z )
              /\ ( I ` ( x H y ) ) = ( y H x ) ) ) ) ) $=
      ( vk vp vt vi vv va vs vh vf cprehil wcel clvec cv wceq wa co wi cfv w3a
      wral wex cbs cplusg cmulr cstv cvbase cvadd cvsca cipr c0g czv fveq2
      syl6eqr eqeq2d anbi12d imbi12d 3anbi2d anbi2d 2ralbidv 3anbi123d 4exbidv
      df-prehil elrab2 cvv fvex eqeltri eleq2 anbi1d ralbidv raleqbi1dv opreq
      3anbi1d opreq1d fveq1 eqeq1d 3anbi3d raleq eleq1d opreq2d opreq12d
      eqeq12d imbi1d fveq2d ceqsex8v anbi2i bitri ) MUOUPMUQUPZUFURZJUSZUGURZEU
      SZUTZUHURZGUSZUIURZIUSZUTZUTZUJURZLUSZUKURZDUSZUTZULURZFUSZUMURZHUSZUTZUT
      ZAURZBURZYKVAZXMUPZOURZYOYIVAZYPYFVAZCURZYKVAZYSYOUUBYKVAZXRVAZYPUUBYKVAZ
      XOVAZUSZYOYOYKVAZKUSZYONUSZVBZYQXTVCZYPYOYKVAZUSZVDZUTZCYDVEBYDVEZAYDVEZO
      XMVEZVDZUMVFULVFUKVFUJVFZUIVFUHVFUGVFUFVFZUTXLYOYPHVAZJUPZYSYOFVAZYPDVAZU
      UBHVAZYSYOUUBHVAZGVAZYPUUBHVAZEVAZUSZYOYOHVAZKUSZUUKVBZUVDIVCZYPYOHVAZUSZ
      VDZUTZCLVEBLVEZALVEOJVEZUTXMUNURZVGVCZUSZXOUWDVHVCZUSZUTZXRUWDVIVCZUSZXTU
      WDVJVCZUSZUTZUTZYDUWDVKVCZUSZYFUWDVLVCZUSZUTZYIUWDVMVCZUSZYKUWDVNVCZUSZUT
      ZUTZYRUUHUUIUWDVOVCZUSZYOUWDVPVCZUSZVBZUUOVDZUTZCYDVEBYDVEZAYDVEOXMVEZVDZ
      UMVFULVFUKVFUJVFZUIVFUHVFUGVFUFVFUVCUNMUQUOUWDMUSZUXQUVBUFUGUHUIUXRUXPUVA
      UJUKULUMUXRUWOYCUXFYNUXOUUTUXRUWIXQUWNYBUXRUWFXNUWHXPUXRUWEJXMUXRUWEMVGVC
      ZJUWDMVGVQPVRVSUXRUWGEXOUXRUWGMVHVCZEUWDMVHVQQVRVSVTUXRUWKXSUWMYAUXRUWJGX
      RUXRUWJMVIVCZGUWDMVIVQRVRVSUXRUWLIXTUXRUWLMVJVCZIUWDMVJVQSVRVSVTVTUXRUWTY
      HUXEYMUXRUWQYEUWSYGUXRUWPLYDUXRUWPMVKVCZLUWDMVKVQTVRVSUXRUWRDYFUXRUWRMVLV
      CZDUWDMVLVQUAVRVSVTUXRUXBYJUXDYLUXRUXAFYIUXRUXAMVMVCZFUWDMVMVQUBVRVSUXRUX
      CHYKUXRUXCMVNVCZHUWDMVNVQUCVRVSVTVTUXRUXNUUROAXMYDUXRUXMUUQBCYDYDUXRUXLUU
      PYRUXRUXKUULUUHUUOUXRUXHUUJUXJUUKUXRUXGKUUIUXRUXGMVOVCKUWDMVOVQUDVRVSUXRU
      XINYOUXRUXIMVPVCNUWDMVPVQUEVRVSWAWBWCWDWDWEWFWFABCUJUHUNUMUIUFULOUGUKWGWH
      UVCUWCXLUUTYQJUPZUUPUTZCYDVEBYDVEZAYDVEZOJVEUYGUUCUUEUUFEVAZUSZUULUUOVDZU
      TZCYDVEBYDVEZAYDVEOJVEUYGUUCYSUUDGVAZUUFEVAZUSZUULUUOVDZUTZCYDVEBYDVEZAYD
      VEOJVEUYGUYRUULYQIVCZUUNUSZVDZUTZCYDVEZBYDVEZAYDVEZOJVEVUECLVEZBLVEZALVEZ
      OJVEUYGYTYPDVAZUUBYKVAZUYQUSZUULVUCVDZUTZCLVEBLVEZALVEOJVEUYGUVGUUBYKVAZU
      YQUSZUULVUCVDZUTZCLVEBLVEZALVEOJVEUWCUFUGUHUIUJUKULJEGILDFHUMJUXSWIPMVGWJ
      WKEUXTWIQMVHWJWKGUYAWIRMVIWJWKIUYBWISMVJWJWKLUYCWITMVKWJWKDUYDWIUAMVLWJWK
      FUYEWIUBMVMWJWKHUYFWIUCMVNWJWKUUSUYJOXMJXNUURUYIAYDXNUUQUYHBCYDYDXNYRUYGU
      UPXMJYQWLWMWDWNWOXPUYIUYOOAJYDXPUYHUYNBCYDYDXPUUPUYMUYGXPUUHUYLUULUUOXPUU
      GUYKUUCUUEUUFXOEWPVSWQWCWDWDXSUYOVUAOAJYDXSUYNUYTBCYDYDXSUYMUYSUYGXSUYLUY
      RUULUUOXSUYKUYQUUCXSUUEUYPUUFEYSUUDXRGWPWRVSWQWCWDWDYAVUAVUGOAJYDYAUYTVUE
      BCYDYDYAUYSVUDUYGYAUUOVUCUYRUULYAUUMVUBUUNYQXTIWSWTXAWCWDWDYEVUHVUKOJVUGV
      UJAYDLVUFVUIBYDLVUECYDLXBWOWOWNYGVUJVUQOAJLYGVUEVUPBCLLYGVUDVUOUYGYGUYRVU
      NUULVUCYGUUCVUMUYQYGUUAVULUUBYKYTYPYFDWPWRWTWQWCWDWDYJVUQVVBOAJLYJVUPVVAB
      CLLYJVUOVUTUYGYJVUNVUSUULVUCYJVUMVURUYQYJVULUVGUUBYKYJYTUVFYPDYSYOYIFWPWR
      WRWTWQWCWDWDYLVVBUWBOAJLYLVVAUWABCLLYLUYGUVEVUTUVTYLYQUVDJYOYPYKHWPZXCYLV
      USUVMUULUVPVUCUVSYLVURUVHUYQUVLUVGUUBYKHWPYLUYPUVJUUFUVKEYLUUDUVIYSGYOUUB
      YKHWPXDYPUUBYKHWPXEXFYLUUJUVOUUKYLUUIUVNKYOYOYKHWPWTXGYLVUBUVQUUNUVRYLYQU
      VDIVVCXHYPYOYKHWPXFWEVTWDWDXIXJXK $.
      $( [22-Sep-2011] $)
  $}

  $c +ss $.
  $c ocv $.
  $c CSubSp $.
  $c Hil $.

  $( Extend class notation with subspace sum. $)
  cplusss $a class +ss $.

  $( Extend class notation with orthocomplement of a subspace. $)
  cocv $a class ocv $.

  $( Extend class notation with set of closed subspaces. $)
  ccsubsp $a class CSubSp $.

  $( Extend class notation with class of all Hilbert spaces. $)
  chs $a class Hil $.

  ${
    $d h s t u v w x y $.
    $( Define subspace sum. $)
    df-plusss $a |- +ss = ( v e. _V |-> ( x e. ~P ( vbase ` v ) ,
        y e. ~P ( vbase ` v ) |-> { w e. ( vbase ` v )
            | E. t e. x E. u e. y w = ( t ( vadd ` v ) u ) } ) ) $.

    $( Def. 8.5.27, p. 227. $)
    $( Define orthocomplement of a subspace. $)
    df-ocv $a |- ocv = ( h e. _V |-> ( s e. ~P ( vbase ` h ) |->
   { x e. ( vbase ` h ) | A. y e. s ( x ( ip ` h ) y ) = ( 0v ` h ) } ) ) $.

    $( Def. 8.5.28, p. 226. $)
    $( Define set of closed subspaces. $)
    df-csubsp $a |- CSubSp = ( h e. _V |-> { s
   | ( s C_ ( vbase ` h ) /\ s = ( ( ocv ` h ) ` ( ( ocv ` h ) ` s ) ) ) } ) $.

    $( Def. 8.5.30, p. 228. $)
    $( Define class of all Hilbert spaces.  Based on Proposition 4.5, p. 176,
       Gudrun Kalmbach, Quantum Measures and Spaces, Kluwer, Dordrecht,
       1998. $)
    df-hil $a |- Hil = { h e. PreHil | A. s e. ( CSubSp ` h )
          ( s ( +ss ` h ) ( ( ocv ` h ) ` s ) ) = ( vbase ` h ) } $.
  $}

  ${
    $d h A $.  $d h t u w x y H $.  $d h w x y V $.
    plusssfval.v $e |- V = ( vbase ` H ) $.
    plusssfval.a $e |- A = ( vadd ` H ) $.
    plusssfval.s $e |- S = ( +ss ` H ) $.
    $( The subspace sum operation. $)
    plusssfval $p |- ( H e. B -> S = ( x e. ~P V , y e. ~P V |-> { w e. V
            | E. t e. x E. u e. y w = ( t A u ) } ) ) $=
      ( vh wcel cvv cpw cv co wceq wrex crab cmpt2 elisset cplusss cfv cvbase
      cvadd fveq2 syl6eqr pweq syl opreqd eqeq2d 2rexbidv rabeqbidv
      mpt2eq123dv df-plusss fvex eqeltri pwex mpt2exg mp2an fvmpt syl5eq ) IGOI
      POZHABJQZVGCRZERZDRZFSZTZDBRZUAEARZUAZCJUBZUCZTIGUDVFHIUEUFVQMNIABNRZUGUF
      ZQZVTVHVIVJVRUHUFZSZTZDVMUAEVNUAZCVSUBZUCVQPUEVRITZABVTVTWEVGVGVPWFVSJTVT
      VGTWFVSIUGUFZJVRIUGUIKUJZVSJUKULZWIWFWDVOCVSJWHWFWCVLEDVNVMWFWBVKVHWFWAFV
      IVJWFWAIUHUFFVRIUHUILUJUMUNUOUPUQABCNDEURVGPOZWJVQPOJJWGPKIUGUSUTVAZWKABV
      GVGVPPPVBVCVDVEUL $.
      $( [7-Oct-2011] $)

    $d x y A $.  $d t w x y X $.  $d t u w x y Y $.
    $( The subspace sum operation value. $)
    plusssval $p |- ( ( H e. B /\ X C_ V /\ Y C_ V ) -> ( X S Y ) = { w e. V
            | E. t e. X E. u e. Y w = ( t A u ) } ) $=
      ( vx vy wcel wss co cv wceq wrex crab wa cpw cmpt2 plusssfval opreqd
      cvbase cfv cvv fvex eqeltri elpw2 rexeq rabbidv rexbidv eqid rabex
      ovmpt2 syl2anbr sylan9eq 3impb ) GEPZIHQZJHQZIJFRZASCSBSDRTZBJUAZCIUAZAHU
      BZTVCVDVEUCVFIJNOHUDZVKVGBOSZUAZCNSZUAZAHUBZUEZRZVJVCFVQIJNOABCDEFGHKLMUF
      UGVDIVKPJVKPVRVJTVEIHHGUHUIUJKGUHUKULZUMJHVSUMNOIJVKVKVPVJVQVMCIUAZAHUBVN
      ITVOVTAHVMCVNIUNUOVLJTZVTVIAHWAVMVHCIVGBVLJUNUPUOVQUQVIAHVSURUSUTVAVB $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d h s x y H $.  $d h I $.  $d h s x V $.  $d h Z $.
    ocvfval.v $e |- V = ( vbase ` H ) $.
    ocvfval.i $e |- I = ( ip ` H ) $.
    ocvfval.z $e |- Z = ( 0v ` H ) $.
    ocvfval.o $e |- O = ( ocv ` H ) $.
    $( The orthocomplement operation. $)
    ocvfval $p |- ( H e. A -> O = ( s e. ~P V |->
                          { x e. V | A. y e. s ( x I y ) = Z } ) ) $=
      ( vh wcel cvv cpw cv co wceq wral crab cmpt elisset cocv cfv cvbase cipr
      czv fveq2 syl6eqr pweq syl opreqd eqeq12d ralbidv rabeqbidv mpteq12dv
      df-ocv fvex eqeltri pwex mptexg ax-mp fvmpt syl5eq ) DCODPOZFIGQZARZBRZES
      ZHTZBIRZUAZAGUBZUCZTDCUDVGFDUEUFVPMNDINRZUGUFZQZVIVJVQUHUFZSZVQUIUFZTZBVM
      UAZAVRUBZUCVPPUEVQDTZIVSWEVHVOWFVRGTVSVHTWFVRDUGUFZGVQDUGUJJUKZVRGULUMWFW
      DVNAVRGWHWFWCVLBVMWFWAVKWBHWFVTEVIVJWFVTDUHUFEVQDUHUJKUKUNWFWBDUIUFHVQDUI
      UJLUKUOUPUQURABNIUSVHPOVPPOGGWGPJDUGUTVAVBIVHVOPVCVDVEVFUM $.
      $( [7-Oct-2011] $)

    $d s I $.  $d s x y S $.  $d s Z $.
    $( Value of the orthocomplement of a subset (normally a subspace) of a
       pre-Hilbert space. $)
    ocvval $p |- ( ( H e. A /\ S C_ V )
              -> ( O ` S ) = { x e. V | A. y e. S ( x I y ) = Z } ) $=
      ( vs wcel wss cfv cpw cv co wceq wral crab cmpt ocvfval fveq1d cvbase
      cvv fvex eqeltri elpw2 raleq rabbidv eqid rabex fvmpt sylbir sylan9eq )
      ECOZDHPZDGQDNHRZASBSFTIUAZBNSZUBZAHUCZUDZQZVBBDUBZAHUCZUSDGVFABCEFGHINJKL
      MUEUFUTDVAOVGVIUADHHEUGQUHJEUGUIUJZUKNDVEVIVAVFVCDUAVDVHAHVBBVCDULUMVFUNV
      HAHVJUOUPUQUR $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d h s H $.  $d h O $.  $d h s V $.
    csubspset.v $e |- V = ( vbase ` H ) $.
    csubspset.o $e |- O = ( ocv ` H ) $.
    csubspset.c $e |- C = ( CSubSp ` H ) $.
    $( The set of closed subspaces of a pre-Hilbert space. $)
    csubspset $p |- ( H e. A
             -> C = { s | ( s C_ V /\ s = ( O ` ( O ` s ) ) ) } ) $=
      ( vh wcel cvv cv wss cfv wceq wa cab elisset ccsubsp cvbase cocv fveq2
      syl6eqr sseq2d fveq1d fveq12d eqeq2d anbi12d abbidv df-csubsp cpw fvex
      eqeltri pwex visset elpw anbi1i abbii ssab2 eqsstr3i ssexi fvmpt syl5eq
      syl ) CAKCLKZBFMZENZVGVGDOZDOZPZQZFRZPCASVFBCTOVMIJCVGJMZUAOZNZVGVGVNUBOZ
      OZVQOZPZQZFRVMLTVNCPZWAVLFWBVPVHVTVKWBVOEVGWBVOCUAOZEVNCUAUCGUDUEWBVSVJVG
      WBVRVIVQDWBVQCUBODVNCUBUCHUDZWBVGVQDWDUFUGUHUIUJJFUKVMEULZEEWCLGCUAUMUNUO
      VMVGWEKZVKQZFRWEWGVLFWFVHVKVGEFUPUQURUSVKFWEUTVAVBVCVDVE $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d s H $.  $d s O $.  $d s S $.  $d s V $.
    iscsubsp.v $e |- V = ( vbase ` H ) $.
    iscsubsp.o $e |- O = ( ocv ` H ) $.
    iscsubsp.c $e |- C = ( CSubSp ` H ) $.
    $( The predicate "is a closed subspace" (of a pre-Hilbert space). $)
    iscsubsp $p |- ( H e. A
              -> ( S e. C <-> ( S C_ V /\ S = ( O ` ( O ` S ) ) ) ) ) $=
      ( vs wcel cv wss cfv wceq wa cab csubspset eleq2d cvv cvbase fvex
      eqeltri ssex adantr sseq1 id fveq2 fveq2d eqeq12d anbi12d elab3 syl6bb )
      DAKZCBKCJLZFMZUOUOENZENZOZPZJQZKCFMZCCENZENZOZPZUNBVACABDEFJGHIRSUTVFJCVB
      CTKVECFFDUANTGDUAUBUCUDUEUOCOZUPVBUSVEUOCFUFVGUOCURVDVGUGVGUQVCEUOCEUHUIU
      JUKULUM $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d h s C $.  $d h s H $.  $d h O $.  $d h S $.  $d h V $.
    ishil.v $e |- V = ( vbase ` H ) $.
    ishil.s $e |- S = ( +ss ` H ) $.
    ishil.o $e |- O = ( ocv ` H ) $.
    ishil.c $e |- C = ( CSubSp ` H ) $.
    $( The predicate "is a Hilbert space" (over a *-division ring). $)
    ishil $p |- ( H e. Hil
        <-> ( H e. PreHil /\ A. s e. C ( s S ( O ` s ) ) = V ) ) $=
      ( vh cv cocv cfv cplusss co cvbase wceq ccsubsp wral cprehil chs fveq2
      syl6eqr fveq1d opreq2d opreqd eqtrd eqeq12d raleqbidv df-hil elrab2 ) FLZ
      UMKLZMNZNZUNONZPZUNQNZRZFUNSNZTUMUMDNZBPZERZFATKCUAUBUNCRZUTVDFVAAVEVACSN
      AUNCSUCJUDVEURVCUSEVEURUMVBUQPVCVEUPVBUMUQVEUMUODVEUOCMNDUNCMUCIUDUEUFVEU
      QBUMVBVEUQCONBUNCOUCHUDUGUHVEUSCQNEUNCQUCGUDUIUJKFUKUL $.
      $( [7-Oct-2011] $)
  $}

