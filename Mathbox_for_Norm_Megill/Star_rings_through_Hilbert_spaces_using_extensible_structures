$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
     Star rings through Hilbert spaces using extensible structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( $c C_rng $. $)
  $( Extend class notation subring. $)
  $( csrng $a class C_rng $. $)

  ${
    $d r s x y $.
    $( Def. 8.5.8, p. 216. $)
    $( Define ` s ` is a subring of ` r ` . $)
    $(
    df-subring $a |- C_rng = { <. s , r >. | ( ( Base ` s ) C_ ( Base ` r )
        /\ A. x e. ( Base ` s ) A. y e. ( Base ` s ) ( ( x ( +g ` s ) y )
  = ( x ( +g ` r ) y ) /\ ( x ( .r ` s ) y ) = ( x ( .r ` r ) y ) ) ) } $.
    $)
  $}

  $c *r $.
  $c *Ring $.

  $( Extend class notation with involution. $)
  cstv $a class *r $.

  $( Extend class notation with class of all *-rings. $)
  csr $a class *Ring $.

  $( Def. 8.5.23, p. 225. $)
  $( Define the involution function of a *-ring. $)
  df-starv $a |- *r = ( v e. _V |-> ( v ` 4 ) ) $.

  $( Utility theorem: index-independent form of ~ df-starv . $)
  starvid $p |- *r = ( f e. _V |-> ( f ` ( *r ` ndx ) ) ) $=
    ( cstv c4 df-starv 4nn ndxid ) BCAADEF $.
    $( [6-Oct-2013] $)

  $( Utility lemma for constructing a star ring. $)
  srnglem $p |- -. ( *r ` ndx )
        e. { ( Base ` ndx ) , ( +g ` ndx ) , ( .r ` ndx ) } $=
    ( vs cnx cstv cfv cbs cplusg cmulr ctp wcel wceq wne c4 ndxarg 4re neeqtrri
    c1 ltneii eqnetri c2 c3 w3o w3a df-starv 4nn 1re 1lt4 basendx 2lt4 df-plusg
    wn 2re 2nn 3re 3lt4 df-mulr 3nn 3pm3.2i ne3anior mpbi fvex eltp mtbir ) BCD
    ZBEDZBFDZBGDZHIVCVDJVCVEJVCVFJUAZVCVDKZVCVEKZVCVFKZUBVGUJVHVIVJVCLVDCLAAUCU
    DMZLPVDPLUENUFQUGORVCLVEVKLSVESLUKNUHQFSAAUIULMORVCLVFVKLTVFTLUMNUNQGTAAUOU
    PMORUQVCVDVCVEVCVFURUSVCVDVEVFBCUTVAVB $.
    $( [18-Nov-2013] $)

  ${
    srngfn.w $e |- W = ( { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. ,
         <. ( .r ` ndx ) , T >. } u. { <. ( *r ` ndx ) , C >. } ) $.
    ${
      srngfn.d $e |- D = ( { ( Base ` ndx ) , ( +g ` ndx ) , ( .r ` ndx ) }
                  u. { ( *r ` ndx ) } ) $.
      $( A constructed star ring is a function with domain ` D ` . $)
      srngfn $p |- W Fn D $=
        ( cnx cbs cfv cplusg cmulr ctp cop cstv fvex eqid rngfn srnglem fnunsn
        ) IJKZILKZIMKZNZCUBAOUCDOUDEONZFIPKBIPQAUEDUFEUFRUERSGHTUA $.
        $( [18-Nov-2013] $)
    $}

    $( The base set of a constructed left vector space. $)
    srngbase $p |- ( B e. X -> B = ( Base ` W ) ) $=
      ( vs cbs cnx cfv cop cplusg cmulr ctp cstv csn cun ax-mp wcel cvv snexALT
      tpex unex eqeltri wfun eqid srngfn fnfun baseid opex tpid1 ssun1 eleqtrri
      wfn sseli strfv ) AEIFHEJIKZALZJMKZCLZJNKZDLZOZJPKZBLZQZRZUAGVDVGUSVAVCUC
      VFUBUDUEEURUTVBOVEQRZUOEUFABVICDEGVIUGUHVIEUISHUJUSVHEUSVDTUSVHTUSVAVCURA
      UKULVDVHUSVDVGUMUPSGUNUQ $.
      $( [18-Nov-2013] $)
  $}

  ${
    $d f i k p q r t $.
    $( Def. 8.5.6 p. 216 $)
    $( Define class of all star rings.  A star ring is a ring with an
       involution (conjugation) function.  Involution (unlike say the ring
       zero) is not unique and therefore must be added as a new component to
       the ring.  For example, two possible involutions for complex numbers are
       the identity function and complex conjugation.  Definition of involution
       in [Holland95] p. 204. $)
    df-starrng $a |- *Ring = { f e. Ring |
       E. k E. p E. t E. i ( ( k = ( Base ` f ) /\ p = ( +g ` f ) )
          /\ ( t = ( .r ` f ) /\ i = ( *r ` f ) ) /\
         A. q e. k A. r e. k ( ( i ` q ) e. k
              /\ ( ( i ` ( q p r ) ) = ( ( i ` q ) p ( i ` r ) )
              /\ ( i ` ( q t r ) ) = ( ( i ` r ) t ( i ` q ) )
              /\ ( i ` ( i ` q ) ) = q ) ) ) } $.
  $}

  ${
    $d f i k p q r t I $.  $d f i k p q r t K $.  $d f i k p q r t P $.
    $d f i k p q r t R $.  $d f i k p q r t T $.
    issrng.k $e |- K = ( Base ` R ) $.
    issrng.p $e |- P = ( +g ` R ) $.
    issrng.t $e |- T = ( .r ` R ) $.
    issrng.i $e |- I = ( *r ` R ) $.
    $( The predicate "is a star ring." $)
    issrng $p |- ( R e. *Ring <-> ( R e. Ring
        /\ A. q e. K A. r e. K ( ( I ` q ) e. K
              /\ ( ( I ` ( q P r ) ) = ( ( I ` q ) P ( I ` r ) )
              /\ ( I ` ( q T r ) ) = ( ( I ` r ) T ( I ` q ) )
              /\ ( I ` ( I ` q ) ) = q ) ) ) ) $=
      ( vk vp cv wceq wa cfv co wral wex vt vi vf csr wcel crg w3a cplusg cmulr
      cstv fveq2 syl6eqr eqeq2d anbi12d 3anbi12d 4exbidv df-starrng elrab2 fvex
      cbs eqeltri anbi1d raleqbi1dv oveq fveq2d eqeq12d 3anbi1d anbi2d 2ralbidv
      cvv eleq2 3anbi2d fveq1 eleq1d oveq12d id fveq12d eqeq1d 3anbi123d anbi2i
      ceqsex4v bitri ) BUDUEBUFUEZLNZEOZMNZAOZPZUANZCOZUBNZDOZPZGNZWKQZWDUEZWNF
      NZWFRZWKQZWOWQWKQZWFRZOZWNWQWIRZWKQZWTWOWIRZOZWOWKQZWNOZUGZPZFWDSZGWDSZUG
      ZUBTUATMTLTZPWCWNDQZEUEZWNWQARZDQZXOWQDQZARZOZWNWQCRZDQZXSXOCRZOZXODQZWNO
      ZUGZPZFESGESZPWDUCNZUTQZOZWFYKUHQZOZPZWIYKUIQZOZWKYKUJQZOZPZXLUGZUBTUATMT
      LTXNUCBUFUDYKBOZUUBXMLMUAUBUUCYPWHUUAWMXLUUCYMWEYOWGUUCYLEWDUUCYLBUTQZEYK
      BUTUKHULUMUUCYNAWFUUCYNBUHQZAYKBUHUKIULUMUNUUCYRWJYTWLUUCYQCWIUUCYQBUIQZC
      YKBUIUKJULUMUUCYSDWKUUCYSBUJQZDYKBUJUKKULUMUNUOUPUAUCUBLFGMUQURXNYJWCXLWO
      EUEZXIPZFESZGESUUHXQWKQZWOWTARZOZXFXHUGZPZFESGESUUHUUMYBWKQZWTWOCRZOZXHUG
      ZPZFESGESYJLMUAUBEACDEUUDVJHBUTUSVAAUUEVJIBUHUSVACUUFVJJBUIUSVADUUGVJKBUJ
      USVAXKUUJGWDEXJUUIFWDEWEWPUUHXIWDEWOVKVBVCVCWGUUIUUOGFEEWGXIUUNUUHWGXBUUM
      XFXHWGWSUUKXAUULWGWRXQWKWNWQWFAVDVEWOWTWFAVDVFVGVHVIWJUUOUUTGFEEWJUUNUUSU
      UHWJXFUURUUMXHWJXDUUPXEUUQWJXCYBWKWNWQWICVDVEWTWOWICVDVFVLVHVIWLUUTYIGFEE
      WLUUHXPUUSYHWLWOXOEWNWKDVMZVNWLUUMYAUURYEXHYGWLUUKXRUULXTXQWKDVMWLWOXOWTX
      SAUVAWQWKDVMZVOVFWLUUPYCUUQYDYBWKDVMWLWTXSWOXOCUVBUVAVOVFWLXGYFWNWLWOXOWK
      DWLVPUVAVQVRVSUNVIWAVTWB $.
      $( [22-Sep-2011] $)
  $}

  ${
    $d x y K $.  $d x y R $.  $d x y ph $.
    $( Ring base $)
    issrngd.k $e |- ( ph -> K = ( Base ` R ) ) $.
    $( Ring plus $)
    issrngd.p $e |- ( ph -> P = ( +g ` R ) ) $.
    $( Ring times $)
    issrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    $( Involution (e.g. complex conjugate) $)
    issrngd.c $e |- ( ph -> C = ( *r ` R ) ) $.
    issrngd.r $e |- ( ph -> R e. Ring ) $.
    $( Closure $)
    issrngd.cl $e |- ( ( ph /\ x e. K ) -> ( C ` x ) e. K ) $.
    $( Distributive law over addition $)
    issrngd.dp $e |- ( ( ph /\ x e. K /\ y e. K )
           -> ( C ` ( x P y ) ) = ( ( C ` x ) P ( C ` y ) ) ) $.
    $( Distributive law over multiplication $)
    issrngd.dt $e |- ( ( ph /\ x e. K /\ y e. K )
           -> ( C ` ( x T y ) ) = ( ( C ` y ) T ( C ` x ) ) ) $.
    $( Idempotency $)
    issrngd.id $e |- ( ( ph /\ x e. K ) -> ( C ` ( C ` x ) ) = x ) $.
    $( Properties that determine a star ring. $)
    issrngd $p |- ( ph -> R e. *Ring ) $=
      ( cfv co wa crg wcel cstv cbs cplusg wceq cmulr w3a wral csr simpl simprl
      cv syl2anc simprr syl3anc jca syl ralrimivva fveq1d eleq12d oveqd fveq12d
      3jca oveq123d eqeq12d eqeq1d 3anbi123d raleqbidv anbi2d mpbid eqid issrng
      anbi12d sylibr ) AFUAUBZBUMZFUCRZRZFUDRZUBZVQCUMZFUERZSZVRRZVSWBVRRZWCSZU
      FZVQWBFUGRZSZVRRZWFVSWISZUFZVSVRRZVQUFZUHZTZCVTUIZBVTUIZTZFUJUBAVPVQDRZHU
      BZVQWBESZDRZXAWBDRZESZUFZVQWBGSZDRZXEXAGSZUFZXADRZVQUFZUHZTZCHUIZBHUIZTWT
      AVPXQMAXOBCHHAVQHUBZWBHUBZTZTZXBXNYAAXRXBAXTUKZAXRXSULZNUNYAXGXKXMYAAXRXS
      XGYBYCAXRXSUOZOUPYAAXRXSXKYBYCYDPUPYAAXRTXMYAAXRYBYCUQQURVDUQUSUQAXQWSVPA
      XPWRBHVTIAXOWQCHVTIAXBWAXNWPAXAVSHVTAVQDVRLUTZIVAAXGWHXKWMXMWOAXDWEXFWGAX
      CWDDVRLAEWCVQWBJVBVCAXAVSXEWFEWCJYEAWBDVRLUTZVEVFAXIWKXJWLAXHWJDVRLAGWIVQ
      WBKVBVCAXEWFXAVSGWIKYFYEVEVFAXLWNVQAXAVSDVRLYEVCVGVHVNVIVIVJVKWCFWIVRVTCB
      VTVLWCVLWIVLVRVLVMVO $.
      $( [18-Nov-2013] $)
  $}

  $c ip $.
  $c PreHilLMx $.

  $( Extend class notation with Hermitian form (inner product). $)
  cipr $a class ip $.

  $( Extend class notation with class all pre-Hilbert spaces. $)
  cprehilLMOLD $a class PreHilLMx $.

  $( Def. 8.5.25, p. 226. $)
  $( Define Hermitian form (inner product). $)
  df-inprod $a |- ip = ( v e. _V |-> ( v ` 8 ) ) $.

  $( Utility theorem: index-independent form of ~ df-ip . $)
  ipid $p |- ip = ( f e. _V |-> ( f ` ( ip ` ndx ) ) ) $=
    ( cipr c8 df-inprod 8nn ndxid ) BCAADEF $.
    $( [6-Oct-2013] $)

  $( Utility lemma for constructing a pre-Hilbert space. $)
  phllem1LMOLD $p |- -. ( *r ` ndx ) e. ( { ( Base ` ndx ) , ( +g ` ndx ) ,
   ( .r ` ndx ) } u. { ( vbaseLMx ` ndx ) ,
   ( vaddLMx ` ndx ) , ( vscaLMx ` ndx ) } ) $=
    ( vs cnx cstv cfv cplusg wcel wceq wne c4 ndxarg ltneii neeqtrri eqnetri c2
    4re c3 c5 necomi c6 c7 cbs ctp cvbaseLMOLD cvaddLMOLD cvscaLMOLD cun w3o wo
    cmulr w3a wn df-starv 4nn c1 1re 1lt4 basendx 2re 2lt4 df-plusg 2nn df-mulr
    3re 3lt4 3nn 3pm3.2i ne3anior mpbi df-vbaseLMOLD 5nn 4lt5 df-vaddLMOLD 4lt6
    5re 6nn 6re df-vscaLMOLD 7nn 7re 4lt7 pm3.2ni elun fvex orbi12i bitri mtbir
    eltp ) BCDZBUADZBEDZBUIDZUBZBUCDZBUDDZBUEDZUBZUFFZWHWIGWHWJGWHWKGUGZWHWMGWH
    WNGWHWOGUGZUHZWRWSWHWIHZWHWJHZWHWKHZUJWRUKXAXBXCWHIWICIAAULUMJZIUNWIUNIUOOU
    PKUQLMWHIWJXDINWJNIUROUSKENAAUTVAJLMWHIWKXDIPWKPIVCOVDKUIPAAVBVEJLMVFWHWIWH
    WJWHWKVGVHWHWMHZWHWNHZWHWOHZUJWSUKXEXFXGWMWHWMQWHUCQAAVIVJJQIWHIQOVNVKKXDLM
    RWNWHWNSWHUDSAAVLVOJSIWHISOVPVMKXDLMRWOWHWOTWHUETAAVQVRJTIWHITOVSVTKXDLMRVF
    WHWMWHWNWHWOVGVHWAWQWHWLFZWHWPFZUHWTWHWLWPWBXHWRXIWSWHWIWJWKBCWCZWGWHWMWNWO
    XJWGWDWEWF $.
    $( [30-Sep-2013] $)

  $( Utility lemma for constructing a pre-Hilbert space. $)
  phllem2LMOLD $p |- -. ( ip ` ndx ) e. ( ( { ( Base ` ndx ) , ( +g ` ndx ) ,
   ( .r ` ndx ) } u. { ( vbaseLMx ` ndx ) ,
       ( vaddLMx ` ndx ) , ( vscaLMx ` ndx ) } ) u. { ( *r ` ndx ) } ) $=
    ( vs cnx cipr cfv wcel wceq wo wne wn c8 ndxarg 8re ltneii neeqtrri eqnetri
    c2 c3 mpbi c5 c6 cbs cplusg cmulr ctp cvbaseLMOLD cvaddLMOLD cvscaLMOLD cun
    cstv csn w3o w3a df-inprod 8nn c1 1re 1lt8 basendx 2re df-plusg 2nn df-mulr
    2lt8 3re 3lt8 3nn 3pm3.2i ne3anior 5re 5lt8 df-vbaseLMOLD 6lt8 df-vaddLMOLD
    5nn 6re 6nn c7 7re 7lt8 df-vscaLMOLD 7nn pm3.2ni c4 4re 4lt8 df-starv df-ne
    4nn elun fvex eltp orbi12i bitri elsnc mtbir ) BCDZBUADZBUBDZBUCDZUDZBUEDZB
    UFDZBUGDZUDZUHZBUIDZUJZUHEZWPWQFWPWRFWPWSFUKZWPXAFWPXBFWPXCFUKZGZWPXFFZGZXK
    XLXIXJWPWQHZWPWRHZWPWSHZULXIIXNXOXPWPJWQCJAAUMUNKZJUOWQUOJUPLUQMURNOWPJWRXQ
    JPWRPJUSLVCMUBPAAUTVAKNOWPJWSXQJQWSQJVDLVEMUCQAAVBVFKNOVGWPWQWPWRWPWSVHRWPX
    AHZWPXBHZWPXCHZULXJIXRXSXTWPJXAXQJSXASJVILVJMUESAAVKVNKNOWPJXBXQJTXBTJVOLVL
    MUFTAAVMVPKNOWPJXCXQJVQXCVQJVRLVSMUGVQAAVTWAKNOVGWPXAWPXBWPXCVHRWBWPXFHXLIW
    PJXFXQJWCXFWCJWDLWEMUIWCAAWFWHKNOWPXFWGRWBXHWPXEEZWPXGEZGXMWPXEXGWIYAXKYBXL
    YAWPWTEZWPXDEZGXKWPWTXDWIYCXIYDXJWPWQWRWSBCWJZWKWPXAXBXCYEWKWLWMWPXFYEWNWLW
    MWO $.
    $( [30-Sep-2013] $)

  ${
    phlfn.h $e |- H = ( ( { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. ,
               <. ( .r ` ndx ) , T >. }
            u. { <. ( vbaseLMx ` ndx ) , V >. , <. ( vaddLMx ` ndx ) , A >. ,
               <. ( vscaLMx ` ndx ) , S >. } )
         u. { <. ( *r ` ndx ) , R >. , <. ( ip ` ndx ) , I >. } ) $.
    ${
      phlfn.d $e |- D = ( ( { ( Base ` ndx ) , ( +g ` ndx ) , ( .r ` ndx ) }
           u. { ( vbaseLMx ` ndx ) , ( vaddLMx ` ndx ) , ( vscaLMx ` ndx ) } )
        u. { ( *r ` ndx ) , ( ip ` ndx ) } ) $.
      $( A constructed pre-Hilbert space is a function.  Starting from
         ~ lmodfnLMOLD (which has 6 members), we chain ~ fnunsn twice, each
         time adding an ordered pair to the function, to get all 8 members. $)
      phlfnLMOLD $p |- H Fn D $=
        ( cnx cfv ctp cun cstv csn cop eqid cplusg cmulr cvbaseLMOLD cvaddLMOLD
        cvscaLMOLD cipr fvex lmodfnLMOLD phllem1LMOLD fnunsn df-pr uneq2i unass
        cbs cpr 3eqtr4i phllem2LMOLD ) MUNNZMUANZMUBNZOMUCNZMUDNZMUENZOPZMQNZRZ
        PZCURBSUSDSUTGSOVAJSVBASVCFSOPZVEESZRZPZHMUFNZIMUFUGVDVGVHVKVEEMQUGABVD
        DFGJVHVHTVDTUHVKTVGTUIUJVHVIVLISZUOZPVHVJVMRZPZPHVKVOPVNVPVHVIVMUKULKVH
        VJVOUMUPVDVEVLUOZPVDVFVLRZPZPCVGVRPVQVSVDVEVLUKULLVDVFVRUMUPUQUJ $.
        $( [1-Oct-2013] $)
    $}

    $( Lemma to shorten proofs of ~ phlbaseLMOLD through ~ phlipLMOLD . $)
    phlfunLMOLD $p |- Fun H $=
      ( cnx cbs cfv cplusg cmulr ctp cvbaseLMOLD cvaddLMOLD cvscaLMOLD cun cstv
      cipr cpr wfn wfun eqid phlfnLMOLD fnfun ax-mp ) GKLMKNMKOMPKQMKRMKSMPTKUA
      MKUBMUCTZUDGUEABUJCDEFGHIJUJUFUGUJGUHUI $.
      $( [6-Oct-2013] $)

    $( The base set of a constructed pre-Hilbert space. $)
    phlbaseLMOLD $p |- ( B e. X -> B = ( Base ` H ) ) $=
      ( vs cbs cnx cfv cop ctp cun tpex unex cplusg cmulr cvaddLMOLD cvscaLMOLD
      cvbaseLMOLD cstv cipr cpr cvv prex eqeltri phlfunLMOLD baseid ssun1 sstri
      opex tpid1 sselii eleqtrri strfv ) BGMJLGNMOZBPZNUAOCPZNUBOFPZQZNUEOIPZNU
      COAPZNUDOEPZQZRZNUFODPZNUGOHPZUHZRZUIKVJVMVEVIVBVCVDSVFVGVHSTVKVLUJTUKABC
      DEFGHIKULLUMVBVNGVEVNVBVEVJVNVEVIUNVJVMUNUOVBVCVDVABUPUQURKUSUT $.
      $( [6-Oct-2013] $)

    $( The additive operation of a constructed pre-Hilbert space. $)
    phlplusgLMOLD $p |- ( P e. X -> P = ( +g ` H ) ) $=
      ( vs cplusg cnx cfv cop ctp cun tpex unex cbs cmulr cvaddLMOLD cvscaLMOLD
      cvbaseLMOLD cstv cipr cpr cvv prex eqeltri phlfunLMOLD plusgid ssun1 opex
      sstri tpid2 sselii eleqtrri strfv ) CGMJLGNUAOBPZNMOZCPZNUBOFPZQZNUEOIPZN
      UCOAPZNUDOEPZQZRZNUFODPZNUGOHPZUHZRZUIKVJVMVEVIVAVCVDSVFVGVHSTVKVLUJTUKAB
      CDEFGHIKULLUMVCVNGVEVNVCVEVJVNVEVIUNVJVMUNUPVAVCVDVBCUOUQURKUSUT $.
      $( [6-Oct-2013] $)

    $( The multiplicative operation of a constructed pre-Hilbert space. $)
    phlmulrLMOLD $p |- ( T e. X -> T = ( .r ` H ) ) $=
      ( vs cmulr cnx cfv cop ctp cun tpex unex cbs cplusg cvaddLMOLD cvscaLMOLD
      cvbaseLMOLD cstv cipr cpr cvv prex eqeltri phlfunLMOLD mulrid ssun1 sstri
      opex tpid3 sselii eleqtrri strfv ) FGMJLGNUAOBPZNUBOCPZNMOZFPZQZNUEOIPZNU
      COAPZNUDOEPZQZRZNUFODPZNUGOHPZUHZRZUIKVJVMVEVIVAVBVDSVFVGVHSTVKVLUJTUKABC
      DEFGHIKULLUMVDVNGVEVNVDVEVJVNVEVIUNVJVMUNUOVAVBVDVCFUPUQURKUSUT $.
      $( [6-Oct-2013] $)

    $( The set of vectors in a constructed pre-Hilbert space. $)
    phlvbaseLMOLD $p |- ( V e. X -> V = ( vbaseLMx ` H ) ) $=
      ( vs cvbaseLMOLD cnx cfv cop ctp cun tpex unex cbs cplusg cvaddLMOLD cstv
      cmulr cvscaLMOLD cipr cpr cvv prex eqeltri phlfunLMOLD vbaseidLMOLD ssun2
      ssun1 sstri opex tpid1 sselii eleqtrri strfv ) IGMJLGNUAOBPZNUBOCPZNUEOFP
      ZQZNMOZIPZNUCOAPZNUFOEPZQZRZNUDODPZNUGOHPZUHZRZUIKVKVNVEVJVBVCVDSVGVHVIST
      VLVMUJTUKABCDEFGHIKULLUMVGVOGVJVOVGVJVKVOVJVEUNVKVNUOUPVGVHVIVFIUQURUSKUT
      VA $.
      $( [6-Oct-2013] $)

    $( The vector addition operation of a constructed pre-Hilbert space. $)
    phlvaddLMOLD $p |- ( A e. X -> A = ( vaddLMx ` H ) ) $=
      ( vs cvaddLMOLD cnx cfv cop ctp cun tpex unex cbs cplusg cmulr cvscaLMOLD
      cvbaseLMOLD cstv cipr cpr cvv eqeltri phlfunLMOLD vaddidLMOLD ssun2 ssun1
      prex sstri opex tpid2 sselii eleqtrri strfv ) AGMJLGNUAOBPZNUBOCPZNUCOFPZ
      QZNUEOIPZNMOZAPZNUDOEPZQZRZNUFODPZNUGOHPZUHZRZUIKVKVNVEVJVBVCVDSVFVHVISTV
      LVMUOTUJABCDEFGHIKUKLULVHVOGVJVOVHVJVKVOVJVEUMVKVNUNUPVFVHVIVGAUQURUSKUTV
      A $.
      $( [6-Oct-2013] $)

    $( The scalar product operation of a constructed pre-Hilbert space. $)
    phlvscaLMOLD $p |- ( S e. X -> S = ( vscaLMx ` H ) ) $=
      ( vs cvscaLMOLD cnx cfv cop ctp cun tpex unex cbs cplusg cmulr cvaddLMOLD
      cvbaseLMOLD cstv cipr cpr cvv eqeltri phlfunLMOLD vscaidLMOLD ssun2 ssun1
      prex sstri opex tpid3 sselii eleqtrri strfv ) EGMJLGNUAOBPZNUBOCPZNUCOFPZ
      QZNUEOIPZNUDOAPZNMOZEPZQZRZNUFODPZNUGOHPZUHZRZUIKVKVNVEVJVBVCVDSVFVGVISTV
      LVMUOTUJABCDEFGHIKUKLULVIVOGVJVOVIVJVKVOVJVEUMVKVNUNUPVFVGVIVHEUQURUSKUTV
      A $.
      $( [6-Oct-2013] $)

    $( The involution operation of a constructed pre-Hilbert space. $)
    phlstarvLMOLD $p |- ( R e. X -> R = ( *r ` H ) ) $=
      ( vs cstv cnx cfv cop ctp cun tpex unex cbs cplusg cvbaseLMOLD cvaddLMOLD
      cmulr cvscaLMOLD cipr cpr cvv prex eqeltri phlfunLMOLD starvid ssun2 opex
      prid1 sselii eleqtrri strfv ) DGMJLGNUAOBPZNUBOCPZNUEOFPZQZNUCOIPZNUDOAPZ
      NUFOEPZQZRZNMOZDPZNUGOHPZUHZRZUIKVHVLVCVGUTVAVBSVDVEVFSTVJVKUJTUKABCDEFGH
      IKULLUMVJVMGVLVMVJVLVHUNVJVKVIDUOUPUQKURUS $.
      $( [6-Oct-2013] $)

    $( The inner product (Hermitian form) operation of a constructed
       pre-Hilbert space. $)
    phlipLMOLD $p |- ( I e. X -> I = ( ip ` H ) ) $=
      ( vs cipr cnx cfv cop ctp cun tpex unex cbs cplusg cvbaseLMOLD cvaddLMOLD
      cmulr cvscaLMOLD cstv cpr prex eqeltri phlfunLMOLD ipid ssun2 opex sselii
      cvv prid2 eleqtrri strfv ) HGMJLGNUAOBPZNUBOCPZNUEOFPZQZNUCOIPZNUDOAPZNUF
      OEPZQZRZNUGODPZNMOZHPZUHZRZUPKVHVLVCVGUTVAVBSVDVEVFSTVIVKUITUJABCDEFGHIKU
      KLULVKVMGVLVMVKVLVHUMVIVKVJHUNUQUOKURUS $.
      $( [6-Oct-2013] $)
  $}

  ${
    $d a f h i k p q r s t v w x y z $.
    $( Def. 8.5.24, p. 225. $)
    $( Define class all generalized pre-Hilbert (inner product) spaces. $)
    df-prehilLMOLD $a |- PreHilLMx = { f e. ( LVecLMx i^i *Ring ) |
       E. k E. p E. t E. i E. v E. a E. s E. h (
        ( ( k = ( Base ` f ) /\ p = ( +g ` f ) )
              /\ ( t = ( .r ` f ) /\ i = ( *r ` f ) ) )
        /\ ( ( v = ( vbaseLMx ` f ) /\ a = ( vaddLMx ` f ) )
              /\ ( s = ( vscaLMx ` f ) /\ h = ( ip ` f ) ) )
          /\ A. q e. k A. x e. v A. y e. v A. z e. v (
            ( x h y ) e. k
              /\ ( ( ( ( q s x ) a y ) h z )
                  = ( ( q t ( x h z ) ) p ( y h z ) )
        /\ ( ( x h x ) = ( 0g ` f ) -> x = ( 0vLMx ` f ) )
              /\ ( i ` ( x h y ) ) = ( y h x ) ) ) ) } $.
  $}

  ${
    $d a f h i k p q s t v x y z A $.  $d a f h i k p q s t v x y z H $.
    $d a f h i k p q s t v x y z I $.  $d a f h i k p q s t v x y z K $.
    $d a f h i k p s t v N $.  $d a f h i k p q s t v x y z P $.
    $d a f h i k p q s t v x y z S $.  $d a f h i k p q s t v x y z T $.
    $d a f h i k p q s t v x y z V $.  $d a f h i k p q s t v x y z W $.
    $d a f h i k p s t v Z $.
    isphil.k $e |- K = ( Base ` W ) $.
    isphil.p $e |- P = ( +g ` W ) $.
    isphil.t $e |- T = ( .r ` W ) $.
    isphil.i $e |- I = ( *r ` W ) $.
    isphil.v $e |- V = ( vbaseLMx ` W ) $.
    isphil.a $e |- A = ( vaddLMx ` W ) $.
    isphil.s $e |- S = ( vscaLMx ` W ) $.
    isphil.h $e |- H = ( ip ` W ) $.
    isphil.n $e |- N = ( 0g ` W ) $.
    isphil.z $e |- Z = ( 0vLMx ` W ) $.
    $( The predicate "is a generalized pre-Hilbert (inner product) space". $)
    isphilLMOLD $p |- ( W e. PreHilLMx <-> ( W e. LVecLMx /\ W e. *Ring
       /\ A. q e. K A. x e. V A. y e. V A. z e. V ( ( x H y ) e. K
              /\ ( ( ( ( q S x ) A y ) H z )
                  = ( ( q T ( x H z ) ) P ( y H z ) )
         /\ ( ( x H x ) = N -> x = Z )
              /\ ( I ` ( x H y ) ) = ( y H x ) ) ) ) ) $=
      ( vk vp vt vi vv va vs vh vf cprehilLMOLD wcel clvecLMOLD csr cv wa co wi
      wceq cfv w3a wral wex cin elin anbi1i cplusg cmulr cvbaseLMOLD cvaddLMOLD
      cbs cstv cvscaLMOLD cipr c0g fveq2 syl6eqr eqeq2d anbi12d imbi12d 3anbi2d
      czvLMOLD anbi2d 2ralbidv 4exbidv df-prehilLMOLD elrab2 df-3an 3bitr4i cvv
      3anbi123d fvex eqeltri eleq2 anbi1d ralbidv raleqbi1dv oveq 3anbi1d fveq1
      oveq1d eqeq1d 3anbi3d raleq eleq1d oveq2d oveq12d eqeq12d imbi1d ceqsex8v
      fveq2d 3anbi3i bitri ) MUOUPZMUQUPZMURUPZUFUSZJVCZUGUSZEVCZUTZUHUSZGVCZUI
      USZIVCZUTZUTZUJUSZLVCZUKUSZDVCZUTZULUSZFVCZUMUSZHVCZUTZUTZAUSZBUSZYSVAZYA
      UPZOUSZUUCYQVAZUUDYNVAZCUSZYSVAZUUGUUCUUJYSVAZYFVAZUUDUUJYSVAZYCVAZVCZUUC
      UUCYSVAZKVCZUUCNVCZVBZUUEYHVDZUUDUUCYSVAZVCZVEZUTZCYLVFBYLVFZAYLVFZOYAVFZ
      VEZUMVGULVGUKVGUJVGZUIVGUHVGUGVGUFVGZVEZXSXTUUCUUDHVAZJUPZUUGUUCFVAZUUDDV
      AZUUJHVAZUUGUUCUUJHVAZGVAZUUDUUJHVAZEVAZVCZUUCUUCHVAZKVCZUUSVBZUVMIVDZUUD
      UUCHVAZVCZVEZUTZCLVFBLVFZALVFOJVFZVEMUQURVHZUPZUVKUTXSXTUTZUVKUTXRUVLUWNU
      WOUVKMUQURVIVJYAUNUSZVOVDZVCZYCUWPVKVDZVCZUTZYFUWPVLVDZVCZYHUWPVPVDZVCZUT
      ZUTZYLUWPVMVDZVCZYNUWPVNVDZVCZUTZYQUWPVQVDZVCZYSUWPVRVDZVCZUTZUTZUUFUUPUU
      QUWPVSVDZVCZUUCUWPWFVDZVCZVBZUVCVEZUTZCYLVFBYLVFZAYLVFOYAVFZVEZUMVGULVGUK
      VGUJVGZUIVGUHVGUGVGUFVGUVKUNMUWMUOUWPMVCZUYIUVJUFUGUHUIUYJUYHUVIUJUKULUMU
      YJUXGYKUXRUUBUYGUVHUYJUXAYEUXFYJUYJUWRYBUWTYDUYJUWQJYAUYJUWQMVOVDZJUWPMVO
      VTPWAWBUYJUWSEYCUYJUWSMVKVDZEUWPMVKVTQWAWBWCUYJUXCYGUXEYIUYJUXBGYFUYJUXBM
      VLVDZGUWPMVLVTRWAWBUYJUXDIYHUYJUXDMVPVDZIUWPMVPVTSWAWBWCWCUYJUXLYPUXQUUAU
      YJUXIYMUXKYOUYJUXHLYLUYJUXHMVMVDZLUWPMVMVTTWAWBUYJUXJDYNUYJUXJMVNVDZDUWPM
      VNVTUAWAWBWCUYJUXNYRUXPYTUYJUXMFYQUYJUXMMVQVDZFUWPMVQVTUBWAWBUYJUXOHYSUYJ
      UXOMVRVDZHUWPMVRVTUCWAWBWCWCUYJUYFUVFOAYAYLUYJUYEUVEBCYLYLUYJUYDUVDUUFUYJ
      UYCUUTUUPUVCUYJUXTUURUYBUUSUYJUXSKUUQUYJUXSMVSVDKUWPMVSVTUDWAWBUYJUYANUUC
      UYJUYAMWFVDNUWPMWFVTUEWAWBWDWEWGWHWHWOWIWIABCUJUHUNUMUIUFULOUGUKWJWKXSXTU
      VKWLWMUVKUWLXSXTUVHUUEJUPZUVDUTZCYLVFBYLVFZAYLVFZOJVFUYSUUKUUMUUNEVAZVCZU
      UTUVCVEZUTZCYLVFBYLVFZAYLVFOJVFUYSUUKUUGUULGVAZUUNEVAZVCZUUTUVCVEZUTZCYLV
      FBYLVFZAYLVFOJVFUYSVUJUUTUUEIVDZUVBVCZVEZUTZCYLVFZBYLVFZAYLVFZOJVFVUQCLVF
      ZBLVFZALVFZOJVFUYSUUHUUDDVAZUUJYSVAZVUIVCZUUTVUOVEZUTZCLVFBLVFZALVFOJVFUY
      SUVPUUJYSVAZVUIVCZUUTVUOVEZUTZCLVFBLVFZALVFOJVFUWLUFUGUHUIUJUKULJEGILDFHU
      MJUYKWNPMVOWPWQEUYLWNQMVKWPWQGUYMWNRMVLWPWQIUYNWNSMVPWPWQLUYOWNTMVMWPWQDU
      YPWNUAMVNWPWQFUYQWNUBMVQWPWQHUYRWNUCMVRWPWQUVGVUBOYAJYBUVFVUAAYLYBUVEUYTB
      CYLYLYBUUFUYSUVDYAJUUEWRWSWHWTXAYDVUAVUGOAJYLYDUYTVUFBCYLYLYDUVDVUEUYSYDU
      UPVUDUUTUVCYDUUOVUCUUKUUMUUNYCEXBWBXCWGWHWHYGVUGVUMOAJYLYGVUFVULBCYLYLYGV
      UEVUKUYSYGVUDVUJUUTUVCYGVUCVUIUUKYGUUMVUHUUNEUUGUULYFGXBXEWBXCWGWHWHYIVUM
      VUSOAJYLYIVULVUQBCYLYLYIVUKVUPUYSYIUVCVUOVUJUUTYIUVAVUNUVBUUEYHIXDXFXGWGW
      HWHYMVUTVVCOJVUSVVBAYLLVURVVABYLLVUQCYLLXHXAXAWTYOVVBVVIOAJLYOVUQVVHBCLLY
      OVUPVVGUYSYOVUJVVFUUTVUOYOUUKVVEVUIYOUUIVVDUUJYSUUHUUDYNDXBXEXFXCWGWHWHYR
      VVIVVNOAJLYRVVHVVMBCLLYRVVGVVLUYSYRVVFVVKUUTVUOYRVVEVVJVUIYRVVDUVPUUJYSYR
      UUHUVOUUDDUUGUUCYQFXBXEXEXFXCWGWHWHYTVVNUWKOAJLYTVVMUWJBCLLYTUYSUVNVVLUWI
      YTUUEUVMJUUCUUDYSHXBZXIYTVVKUWBUUTUWEVUOUWHYTVVJUVQVUIUWAUVPUUJYSHXBYTVUH
      UVSUUNUVTEYTUULUVRUUGGUUCUUJYSHXBXJUUDUUJYSHXBXKXLYTUURUWDUUSYTUUQUWCKUUC
      UUCYSHXBXFXMYTVUNUWFUVBUWGYTUUEUVMIVVOXOUUDUUCYSHXBXLWOWCWHWHXNXPXQ $.
      $( [22-Sep-2011] $)
  $}


  ${
    $d q x y z K $.  $d x y z V $.  $d q x y z W $.  $d q x y z ph $.
    $( Division ring (skew field) base $)
    isphild.k $e |- ( ph -> K = ( Base ` W ) ) $.
    $( Division ring (skew field) plus $)
    isphild.p $e |- ( ph -> P = ( +g ` W ) ) $.
    $( Division ring (skew field) times $)
    isphild.t $e |- ( ph -> T = ( .r ` W ) ) $.
    $( Involution (e.g. complex conjugate) $)
    isphild.c $e |- ( ph -> C = ( *r ` W ) ) $.
    $( Set of vectors $)
    isphild.v $e |- ( ph -> V = ( vbaseLMx ` W ) ) $.
    $( Vector addition $)
    isphild.a $e |- ( ph -> A = ( vaddLMx ` W ) ) $.
    $( Scalar product $)
    isphild.s $e |- ( ph -> S = ( vscaLMx ` W ) ) $.
    $( Inner product (Hermitian form) $)
    isphild.i $e |- ( ph -> I = ( ip ` W ) ) $.
    $( Division ring zero $)
    isphild.o $e |- ( ph -> O = ( 0g ` W ) ) $.
    $( Zero vector $)
    isphild.z $e |- ( ph -> Z = ( 0vLMx ` W ) ) $.
    isphild.l $e |- ( ph -> W e. LVecLMx ) $.
    isphild.r $e |- ( ph -> W e. *Ring ) $.
    $( Closure of inner product $)
    isphild.cl $e |- ( ( ph /\ x e. V /\ y e. V ) -> ( x I y ) e. K ) $.
    $( Distributive law for inner product $)
    isphild.d $e |- ( ( ph /\ q e. K /\ ( x e. V /\ y e. V /\ z e. V ) )
          -> ( ( ( q S x ) A y ) I z ) = ( ( q T ( x I z ) ) P ( y I z ) ) ) $.
    $( Nonsingularity $)
    isphild.ns $e |- ( ( ph /\ x e. V /\ ( x I x ) = O ) -> x = Z ) $.
    $( Conjugation law $)
    isphild.cj $e |- ( ( ph /\ x e. V /\ y e. V )
            -> ( C ` ( x I y ) ) = ( y I x ) ) $.
    $( Properties that determine a pre-Hilbert (inner product) space. $)
    isphildLMOLD $p |- ( ph -> W e. PreHilLMx ) $=
      ( clvecLMOLD wcel csr cv cipr cfv co cbs cvscaLMOLD cvaddLMOLD cmulr wceq
      cplusg c0g czvLMOLD cstv w3a cvbaseLMOLD wral cprehilLMOLD simpll simplrr
      wi wa simprl syl3anc simplrl simprr syl113anc simplll adantr simpr ex jca
      ralrimivva oveqd eleq12d eqidd oveq123d eqeq12d imbi12d fveq12d 3anbi123d
      3jca eqeq2d anbi12d raleqbidv 3anbi3d mpbid eqid isphilLMOLD sylibr ) ANU
      MUNZNUOUNZBUPZCUPZNUQURZUSZNUTURZUNZPUPZXGNVAURZUSZXHNVBURZUSZDUPZXIUSZXM
      XGXRXIUSZNVCURZUSZXHXRXIUSZNVEURZUSZVDZXGXGXIUSZNVFURZVDZXGNVGURZVDZVOZXJ
      NVHURZURZXHXGXIUSZVDZVIZVPZDNVJURZVKZCYSVKZBYSVKZPXKVKZVIZNVLUNAXEXFXGXHJ
      USZKUNZXMXGHUSZXHEUSZXRJUSZXMXGXRJUSZIUSZXHXRJUSZGUSZVDZXGXGJUSZLVDZXGOVD
      ZVOZUUEFURZXHXGJUSZVDZVIZVPZDMVKZCMVKZBMVKZPKVKZVIUUDAXEXFUVGUGUHAUVEPBKM
      AXMKUNZXGMUNZVPZVPZUVCCDMMUVKXHMUNZXRMUNZVPZVPZUUFUVBUVOAUVIUVLUUFAUVJUVN
      VMZAUVHUVIUVNVNZUVKUVLUVMVQZUIVRUVOUUNUURUVAUVOAUVHUVIUVLUVMUUNUVPAUVHUVI
      UVNVSUVQUVRUVKUVLUVMVTUJWAUVOUUPUUQUVOUUPVPAUVIUUPUUQAUVJUVNUUPWBUVOUVIUU
      PUVQWCUVOUUPWDUKVRWEUVOAUVIUVLUVAUVPUVQUVRULVRWPWFWGWGWPAUVGUUCXEXFAUVFUU
      BPKXKQAUVEUUABMYSUAAUVDYTCMYSUAAUVCYRDMYSUAAUUFXLUVBYQAUUEXJKXKAJXIXGXHUD
      WHZQWIAUUNYFUURYLUVAYPAUUIXSUUMYEAUUHXQXRXRJXIUDAUUGXOXHXHEXPUBAHXNXMXGUC
      WHAXHWJWKAXRWJWKAUUKYBUULYCGYDRAXMXMUUJXTIYASAXMWJAJXIXGXRUDWHWKAJXIXHXRU
      DWHWKWLAUUPYIUUQYKAUUOYGLYHAJXIXGXGUDWHUEWLAOYJXGUFWQWMAUUSYNUUTYOAUUEXJF
      YMTUVSWNAJXIXHXGUDWHWLWOWRWSWSWSWSWTXABCDXPYDXNYAXIYMXKYHYSNYJPXKXBYDXBYA
      XBYMXBYSXBXPXBXNXBXIXBYHXBYJXBXCXD $.
      $( [18-Nov-2013] $)
  $}


  $c +ssLMx $.
  $c ocvLMx $.
  $c CSubSpLMx $.
  $c HilLMx $.

  $( Extend class notation with subspace sum. $)
  cplusssLMOLD $a class +ssLMx $.

  $( Extend class notation with orthocomplement of a subspace. $)
  cocvLMOLD $a class ocvLMx $.

  $( Extend class notation with set of closed subspaces. $)
  ccsubspLMOLD $a class CSubSpLMx $.

  $( Extend class notation with class of all Hilbert spaces. $)
  chsLMOLD $a class HilLMx $.

  ${
    $d h s t u v w x y $.
    $( Define subspace sum. $)
    df-plusssLMOLD $a |- +ssLMx = ( v e. _V |-> ( x e. ~P ( vbaseLMx ` v ) ,
        y e. ~P ( vbaseLMx ` v ) |-> { w e. ( vbaseLMx ` v )
            | E. t e. x E. u e. y w = ( t ( vaddLMx ` v ) u ) } ) ) $.

    $( Def. 8.5.27, p. 227. $)
    $( Define orthocomplement of a subspace. $)
    df-ocvLMOLD $a |- ocvLMx = ( h e. _V |-> ( s e. ~P ( vbaseLMx ` h ) |->
   { x e. ( vbaseLMx ` h )
     | A. y e. s ( x ( ip ` h ) y ) = ( 0vLMx ` h ) } ) ) $.

    $( Def. 8.5.28, p. 226. $)
    $( Define set of closed subspaces. $)
    df-csubspLMOLD $a |- CSubSpLMx = ( h e. _V |-> { s
   | ( s C_ ( vbaseLMx ` h )
      /\ s = ( ( ocvLMx ` h ) ` ( ( ocvLMx ` h ) ` s ) ) ) } ) $.

    $( Def. 8.5.30, p. 228. $)
    $( Define class of all Hilbert spaces.  Based on Proposition 4.5, p. 176,
       Gudrun Kalmbach, Quantum Measures and Spaces, Kluwer, Dordrecht,
       1998. $)
    df-hilLMOLD $a |- HilLMx = { h e. PreHilLMx | A. s e. ( CSubSpLMx ` h )
          ( s ( +ssLMx ` h ) ( ( ocvLMx ` h ) ` s ) ) = ( vbaseLMx ` h ) } $.
  $}

  ${
    $d h A $.  $d h t u w x y H $.  $d h w x y V $.
    plusssfval.v $e |- V = ( vbaseLMx ` H ) $.
    plusssfval.a $e |- A = ( vaddLMx ` H ) $.
    plusssfval.s $e |- S = ( +ssLMx ` H ) $.
    $( The subspace sum operation. $)
    plusssfvalLMOLD $p |- ( H e. B -> S = ( x e. ~P V , y e. ~P V |-> { w e. V
            | E. t e. x E. u e. y w = ( t A u ) } ) ) $=
      ( wcel cvv cv wceq wrex cfv cvbaseLMOLD vh cpw co crab cmpt2 cplusssLMOLD
      elex cvaddLMOLD fveq2 syl6eqr pweqd eqeq2d 2rexbidv rabeqbidv mpt2eq123dv
      oveqd df-plusssLMOLD fvex eqeltri pwex mpt2exga mp2an fvmpt syl5eq syl )
      IGNIONZHABJUBZVGCPZEPZDPZFUCZQZDBPZREAPZRZCJUDZUEZQIGUGVFHIUFSVQMUAIABUAP
      ZTSZUBZVTVHVIVJVRUHSZUCZQZDVMREVNRZCVSUDZUEVQOUFVRIQZABVTVTWEVGVGVPWFVSJW
      FVSITSZJVRITUIKUJZUKZWIWFWDVOCVSJWHWFWCVLEDVNVMWFWBVKVHWFWAFVIVJWFWAIUHSF
      VRIUHUILUJUPULUMUNUOABCUADEUQVGONZWJVQONJJWGOKITURUSUTZWKABVGVGVPOOVAVBVC
      VDVE $.
      $( [7-Oct-2011] $)

    $d x y A $.  $d t w x y X $.  $d t u w x y Y $.
    $( The subspace sum operation value. $)
    plusssvalLMOLD $p |- ( ( H e. B /\ X C_ V /\ Y C_ V )
    -> ( X S Y ) = { w e. V | E. t e. X E. u e. Y w = ( t A u ) } ) $=
      ( vx vy wcel co cv wceq wrex wss wa cpw cmpt2 plusssfvalLMOLD cvbaseLMOLD
      crab oveqd cfv fvex eqeltri elpw2 rexeq rabbidv rexbidv eqid rabex ovmpt2
      cvv syl2anbr sylan9eq 3impb ) GEPZIHUAZJHUAZIJFQZARCRBRDQSZBJTZCITZAHUGZS
      VCVDVEUBVFIJNOHUCZVKVGBORZTZCNRZTZAHUGZUDZQZVJVCFVQIJNOABCDEFGHKLMUEUHVDI
      VKPJVKPVRVJSVEIHHGUFUIUSKGUFUJUKZULJHVSULNOIJVKVKVPVJVQVMCITZAHUGVNISVOVT
      AHVMCVNIUMUNVLJSZVTVIAHWAVMVHCIVGBVLJUMUOUNVQUPVIAHVSUQURUTVAVB $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d h s x y H $.  $d h I $.  $d h s x V $.  $d h Z $.
    ocvfval.v $e |- V = ( vbaseLMx ` H ) $.
    ocvfval.i $e |- I = ( ip ` H ) $.
    ocvfval.z $e |- Z = ( 0vLMx ` H ) $.
    ocvfval.o $e |- O = ( ocvLMx ` H ) $.
    $( The orthocomplement operation. $)
    ocvfvalLMOLD $p |- ( H e. A -> O = ( s e. ~P V |->
                          { x e. V | A. y e. s ( x I y ) = Z } ) ) $=
      ( vh wcel cvv cv wceq cfv cvbaseLMOLD cpw co wral crab cmpt elex czvLMOLD
      cocvLMOLD fveq2 syl6eqr pweqd oveqd eqeq12d ralbidv rabeqbidv df-ocvLMOLD
      cipr mpteq12dv fvex eqeltri pwex mptexg ax-mp fvmpt syl5eq syl ) DCODPOZF
      IGUAZAQZBQZEUBZHRZBIQZUCZAGUDZUEZRDCUFVGFDUHSVPMNDINQZTSZUAZVIVJVQUQSZUBZ
      VQUGSZRZBVMUCZAVRUDZUEVPPUHVQDRZIVSWEVHVOWFVRGWFVRDTSZGVQDTUIJUJZUKWFWDVN
      AVRGWHWFWCVLBVMWFWAVKWBHWFVTEVIVJWFVTDUQSEVQDUQUIKUJULWFWBDUGSHVQDUGUILUJ
      UMUNUOURABNIUPVHPOVPPOGGWGPJDTUSUTVAIVHVOPVBVCVDVEVF $.
      $( [7-Oct-2011] $)

    $d s I $.  $d s x y S $.  $d s Z $.
    $( Value of the orthocomplement of a subset (normally a subspace) of a
       pre-Hilbert space. $)
    ocvvalLMOLD $p |- ( ( H e. A /\ S C_ V )
              -> ( O ` S ) = { x e. V | A. y e. S ( x I y ) = Z } ) $=
      ( vs wcel cfv cv wceq wral crab wss cpw cmpt ocvfvalLMOLD cvbaseLMOLD cvv
      fveq1d fvex eqeltri elpw2 raleq rabbidv eqid rabex fvmpt sylbir sylan9eq
      co ) ECOZDHUAZDGPDNHUBZAQBQFURIRZBNQZSZAHTZUCZPZVBBDSZAHTZUSDGVFABCEFGHIN
      JKLMUDUGUTDVAOVGVIRDHHEUEPUFJEUEUHUIZUJNDVEVIVAVFVCDRVDVHAHVBBVCDUKULVFUM
      VHAHVJUNUOUPUQ $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d h s H $.  $d h O $.  $d h s V $.
    csubspset.v $e |- V = ( vbaseLMx ` H ) $.
    csubspset.o $e |- O = ( ocvLMx ` H ) $.
    csubspset.c $e |- C = ( CSubSpLMx ` H ) $.
    $( The set of closed subspaces of a pre-Hilbert space. $)
    csubspsetLMOLD $p |- ( H e. A
             -> C = { s | ( s C_ V /\ s = ( O ` ( O ` s ) ) ) } ) $=
      ( vh wcel cvv cv wss cfv wceq wa cab cvbaseLMOLD cocvLMOLD syl6eqr sseq2d
      elex ccsubspLMOLD fveq1d fveq12d eqeq2d anbi12d abbidv df-csubspLMOLD cpw
      fveq2 fvex eqeltri pwex vex elpw anbi1i abbii ssab2 eqsstr3i ssexi syl5eq
      fvmpt syl ) CAKCLKZBFMZENZVGVGDOZDOZPZQZFRZPCAUCVFBCUDOVMIJCVGJMZSOZNZVGV
      GVNTOZOZVQOZPZQZFRVMLUDVNCPZWAVLFWBVPVHVTVKWBVOEVGWBVOCSOZEVNCSULGUAUBWBV
      SVJVGWBVRVIVQDWBVQCTODVNCTULHUAZWBVGVQDWDUEUFUGUHUIJFUJVMEUKZEEWCLGCSUMUN
      UOVMVGWEKZVKQZFRWEWGVLFWFVHVKVGEFUPUQURUSVKFWEUTVAVBVDVCVE $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d s H $.  $d s O $.  $d s S $.  $d s V $.
    iscsubsp.v $e |- V = ( vbaseLMx ` H ) $.
    iscsubsp.o $e |- O = ( ocvLMx ` H ) $.
    iscsubsp.c $e |- C = ( CSubSpLMx ` H ) $.
    $( The predicate "is a closed subspace" (of a pre-Hilbert space). $)
    iscsubspLMOLD $p |- ( H e. A
              -> ( S e. C <-> ( S C_ V /\ S = ( O ` ( O ` S ) ) ) ) ) $=
      ( vs wcel cv wss cfv wceq wa cab csubspsetLMOLD cvv cvbaseLMOLD fvex ssex
      eleq2d eqeltri adantr sseq1 id fveq2 fveq2d eqeq12d anbi12d elab3 syl6bb
      ) DAKZCBKCJLZFMZUOUOENZENZOZPZJQZKCFMZCCENZENZOZPZUNBVACABDEFJGHIRUCUTVFJ
      CVBCSKVECFFDTNSGDTUAUDUBUEUOCOZUPVBUSVEUOCFUFVGUOCURVDVGUGVGUQVCEUOCEUHUI
      UJUKULUM $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d h s C $.  $d h s H $.  $d h O $.  $d h S $.  $d h V $.
    ishil.v $e |- V = ( vbaseLMx ` H ) $.
    ishil.s $e |- S = ( +ssLMx ` H ) $.
    ishil.o $e |- O = ( ocvLMx ` H ) $.
    ishil.c $e |- C = ( CSubSpLMx ` H ) $.
    $( The predicate "is a Hilbert space" (over a *-division ring). $)
    ishilLMOLD $p |- ( H e. HilLMx
        <-> ( H e. PreHilLMx /\ A. s e. C ( s S ( O ` s ) ) = V ) ) $=
      ( vh cocvLMOLD cfv cplusssLMOLD co cvbaseLMOLD ccsubspLMOLD fveq2 syl6eqr
      wceq cv cprehilLMOLD chsLMOLD fveq1d oveq2d oveqd eqtrd eqeq12d raleqbidv
      wral df-hilLMOLD elrab2 ) FUAZUMKUAZLMZMZUNNMZOZUNPMZTZFUNQMZUJUMUMDMZBOZ
      ETZFAUJKCUBUCUNCTZUTVDFVAAVEVACQMAUNCQRJSVEURVCUSEVEURUMVBUQOVCVEUPVBUMUQ
      VEUMUODVEUOCLMDUNCLRISUDUEVEUQBUMVBVEUQCNMBUNCNRHSUFUGVEUSCPMEUNCPRGSUHUI
      KFUKUL $.
      $( [7-Oct-2011] $)
  $}

