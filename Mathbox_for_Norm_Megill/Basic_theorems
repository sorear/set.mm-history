$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Basic theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    q2th.1 $e |- ph $.
    q2th.2 $e |- ps $.
    $( Add theorem antecedent to a theorem. $)
    q2th $p |- ( ps -> ph ) $=
      ( wi ax-q1 ax-qmp ) BBAEZDABHECABFGG $.
      $( [27-Aug-2009] $) $( [8-Aug-2009] $)
  $}

  ${
    qa1i.1 $e |- ph $.
    $( Add an antecedent to a theorem. $)
    qa1i $p |- ( ps -> ph ) $=
      ( wi qid ax-q3 ax-qmp q2th qsyl ) BBBDZAJBJDBEZBBFGAJCKHI $.
      $( [27-Aug-2009] $) $( [8-Aug-2009] $)
  $}

  ${
    qmpc.1 $e |- ph $.
    qmpc.2 $e |- ( -. ph \/ ps ) $.
    $( Classical modus ponens. $)
    qmpc $p |- ps $=
      ( wn qnotnot1 ax-qmp wi wo df-qora ) AEZEZBALCAFGZLLBHZMKBILNHDKBJGGG $.
      $( [1-Aug-2009] $) $( [28-Jul-2009] $)
  $}

  $( An older axiom of quantum logic system Q3, proved from the others,
     so it is redundant. $)
  qax8a $p |- ( ( ph -> ph ) -> ( ps -> ( ph -> ph ) ) ) $=
    ( wi qid qa1i ) BAACZCFFBADEE $.
    $( [1-Aug-2009] $) $( [30-Jul-2009] $)

  $( An older axiom of quantum logic system Q3, proved from the others,
     so it is redundant. $)
  qax8b $p |- ( ( ps -> ( ps -> ( ph -> ph ) ) ) -> ( ph -> ph ) ) $=
    ( wi qid qa1i ) AACZBBFCCADE $.
    $( [1-Aug-2009] $) $( [30-Jul-2009] $)

  ${
    qcon4i.1 $e |- ( -. ph -> -. ps ) $.
    $( Contraposition inference. $)
    qcon4i $p |- ( ps -> ph ) $=
      ( wn wi ax-q8 ax-qmp ) ADBDEZBAEZCHHIECABFGG $.
      $( [6-Aug-2009] $) $( [4-Aug-2009] $)
  $}

  ${
    qcon3i.1 $e |- ( ph -> ps ) $.
    $( Contraposition inference. $)
    qcon3i $p |- ( -. ps -> -. ph ) $=
      ( wn qnotnot2 qnotnot1 qsyl qcon4i ) ADZBDZIDAJDZAEABKCBFGGH $.
      $( [6-Aug-2009] $) $( [4-Aug-2009] $)
  $}

  ${
    qcon1i.1 $e |- ( -. ph -> ps ) $.
    $( Contraposition inference. $)
    qcon1i $p |- ( -. ps -> ph ) $=
      ( wn qcon3i qnotnot2 qsyl ) BDADZDAHBCEAFG $.
      $( [27-Aug-2009] $) $( [5-Aug-2009] $)
  $}

  ${
    qcon2i.1 $e |- ( ph -> -. ps ) $.
    $( Contraposition inference. $)
    qcon2i $p |- ( ps -> -. ph ) $=
      ( wn qnotnot2 qsyl qcon4i ) ADZBHDABDAECFG $.
      $( [27-Aug-2009] $) $( [5-Aug-2009] $)
  $}

  $( Add disjunct to consequent. $)
  qorl $p |- ( ph -> ( ps \/ ph ) ) $=
    ( wn wi wo ax-q1 df-qorb qsyl ) ABCZIADDBAEAIFBAGH $.
    $( [27-Aug-2009] $) $( [5-Aug-2009] $)

  ${
    q2imim2i.1 $e |- ( ph -> ps ) $.
    $( Introduce an imbedded consequent into both sides of an implication. $)
    q2imim2i $p |- ( ( ch -> ( ch -> ph ) ) -> ( ch -> ( ch -> ps ) ) ) $=
      ( wi wn ax-q13 ax-qmp ax-q10 ) CCBEEZFCCAEEZEJEZKJEZABEZLDNNLEDABCGHHZLLM
      EOJKIHH $.
      $( [9-Sep-2009] $) $( [5-Aug-2009] $)
  $}

  ${
    qor2lim.1 $e |- ( -. ph -> ch ) $.
    qor2lim.2 $e |- ( ps -> ch ) $.
    $( Disjoin antecedents (implication version of ~ qor2l ). $)
    qor2lim $p |- ( ( ph -> ( ph -> ps ) ) -> ch ) $=
      ( wi q2imim2i wn ax-q11 ax-qmp qsyl ) AABFFAACFFZCBCAEGAHCFZLCFZDMMNFDACI
      JJK $.
      $( [9-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    qor2l.1 $e |- ( ph -> ch ) $.
    qor2l.2 $e |- ( ps -> ch ) $.
    $( Disjoin antecedents. $)
    qor2l $p |- ( ( ph \/ ps ) -> ch ) $=
      ( wo wn wi df-qora q2imim2i qsyl ax-q4 ax-q11 ax-qmp ) ABFZAGZPCHHZCOPPBH
      HQABIBCPEJKPGZCHZQCHZRACALDKZSSTHUAPCMNNK $.
      $( [9-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    qan2r.1 $e |- ( ph -> ps ) $.
    qan24.2 $e |- ( ph -> ch ) $.
    $( Conjoin consequents. $)
    qan2r $p |- ( ph -> ( ps /\ ch ) ) $=
      ( wn wo wa qcon3i qor2l qcon2i df-qanb qsyl ) ABFZCFZGZFBCHPANOAFABDIACEI
      JKBCLM $.
      $( [9-Sep-2009] $) $( [16-Aug-2009] $)
  $}

  $( Disjunction is commutative. $)
  qorcoma $p |- ( ( ph \/ ps ) -> ( ps \/ ph ) ) $=
    ( wo qorl wn wi qnotnot1 ax-q2 qsyl df-qorb qor2l ) ABBACZABDBBEZMAFFZLBMEN
    BGMAHIBAJIK $.
    $( [9-Sep-2009] $) $( [12-Aug-2009] $)

  $( Conjunction is commutative.

     Note:  see description of quantum logic system Q3 under ~ ax-q1  . $)
  qancoma $p |- ( ( ph /\ ps ) -> ( ps /\ ph ) ) $=
    ( wa wn wo df-qana qorcoma qcon3i df-qanb qsyl ) ABCADZBDZEZDZBACZABFNLKEZD
    OPMLKGHBAIJJ $.
    $( [1-Aug-2009] $) $( [29-Jul-2009] $)

  $( Add disjunct to consequent. $)
  qorr $p |- ( ph -> ( ph \/ ps ) ) $=
    ( wo qorl qorcoma qsyl ) ABACABCABDBAEF $.
    $( [1-Aug-2009] $) $( [5-Aug-2009] $)

  $( Remove conjunct from antecent. $)
  qanr $p |- ( ( ph /\ ps ) -> ph ) $=
    ( wa wn wo df-qana qorr qnotnot1 qsyl qcon4i ) ABCADZBDZEZDZAABFANKMNDKLGMH
    IJI $.
    $( [1-Sep-2009] $) $( [5-Aug-2009] $)

  $( Remove conjunct from antecent. $)
  qanl $p |- ( ( ph /\ ps ) -> ps ) $=
    ( wa qancoma qanr qsyl ) ABCBACBABDBAEF $.
    $( [1-Sep-2009] $) $( [5-Aug-2009] $)

  $( Associative law. $)
  qorassa $p |-  ( ( ( ph \/ ps ) \/ ch ) -> ( ph \/ ( ps \/ ch ) ) ) $=
    ( wo wn wi df-qora qorl qorcoma qsyl q2imim2i df-qorb qor2l ) ABDZCABCDZDZN
    AEZQOFFZPNQQBFFRABGBOQBCBDOBCHCBIJKJAOLZJCACDZPCAHTRPTQQCFFRACGCOQCBHKJSJJM
    $.
    $( [1-Sep-2009] $) $( [12-Aug-2009] $)

  ${
    qorim2i.1 $e |- ( ph -> ps ) $.
    $( Introduce a left disjunct to both sides of an implication. $)
    qorim2i $p |- ( ( ch \/ ph ) -> ( ch \/ ps ) ) $=
      ( wo wn wi df-qora q2imim2i df-qorb qsyl ) CAECFZLAGGZCBEZCAHMLLBGGNABLDI
      CBJKK $.
      $( [3-Sep-2009] $) $( [5-Aug-2009] $)
  $}

  ${
    qorim1i.1 $e |- ( ph -> ps ) $.
    $( Introduce a right disjunct to both sides of an implication. $)
    qorim1i $p |- ( ( ph \/ ch ) -> ( ps \/ ch ) ) $=
      ( wo qorcoma qorim2i qsyl ) ACEZCBEZBCEICAEJACFABCDGHCBFH $.
      $( [3-Sep-2009] $) $( [5-Aug-2009] $)
  $}

  $( Disjunction in terms of implication. $)
  qor2a $p |- ( ( -. ph \/ ps ) -> ( ph -> ( ph -> ps ) ) ) $=
    ( wn wi ax-q2 ax-q1 qor2l ) ACBAABDDABEBAFG $.
    $( [9-Sep-2009] $) $( [3-Sep-2009] $)

  $( Disjunction in terms of implication. $)
  qor2b $p |- ( ( ph -> ( ph -> ps ) ) -> ( -. ph \/ ps ) ) $=
    ( wn wo qorr qorl qor2lim ) ABACZBDHBEBHFG $.
    $( [9-Sep-2009] $) $( [3-Sep-2009] $)

  $( Expand definition of AND. $)
  qdfan2a $p |- ( ( ph /\ ps ) -> -. ( ph -> ( ph -> -. ps ) ) ) $=
    ( wn wi wa wo qor2b df-qana qcon2i qsyl ) AABCZDDZABEZLACKFZMCAKGMNABHIJI
    $.
    $( [9-Sep-2009] $) $( [5-Aug-2009] $)

  $( Expand definition of AND. $)
  qdfan2b $p |- ( -. ( ph -> ( ph -> -. ps ) ) -> ( ph /\ ps ) ) $=
    ( wa wn wi wo df-qanb qcon1i qor2a qsyl ) ABCZAABDZEEZKDADLFZMNKABGHALIJH
    $.
    $( [9-Sep-2009] $) $( [5-Aug-2009] $)

  ${
    qanim1i.1 $e |- ( ph -> ps ) $.
    $( Introduce a right conjunct to both sides of an implication. $)
    qanim1i $p |- ( ( ph /\ ch ) -> ( ps /\ ch ) ) $=
      ( wa wn wo df-qana qcon3i qorim1i qsyl df-qanb ) ACEZBFZCFZGZFZBCEMAFZOGZ
      FQACHPSNROABDIJIKBCLK $.
      $( [9-Sep-2009] $) $( [5-Aug-2009] $)
  $}

  ${
    qanim2i.1 $e |- ( ph -> ps ) $.
    $( Introduce a left conjunct to both sides of an implication. $)
    qanim2i $p |- ( ( ch /\ ph ) -> ( ch /\ ps ) ) $=
      ( wa wn wo df-qana qcon3i qorim2i df-qanb qsyl ) CAECFZAFZGZFZCBEZCAHPMBF
      ZGZFQSORNMABDIJICBKLL $.
      $( [9-Sep-2009] $) $( [5-Aug-2009] $)
  $}

  $( Conjunction implies "quantum conjunction". $)
  qanqan $p |- ( ( ph /\ ps ) -> -. ( ph -> -. ps ) ) $=
    ( wa wn wi qdfan2a ax-q3 qcon3i qsyl ) ABCAABDZEZEZDKDABFKLAJGHI $.
    $( [9-Sep-2009] $) $( [5-Aug-2009] $)

  ${
    qanim12i.1 $e |- ( ph -> ps ) $.
    qanim12i.2 $e |- ( ch -> th ) $.
    $( Join both sides with conjunct. $)
    qanim12i $p |- ( ( ph /\ ch ) -> ( ps /\ th ) ) $=
      ( wa qanim1i qanim2i qsyl ) ACGBCGBDGABCEHCDBFIJ $.
      $( [10-Sep-2009] $) $( [5-Aug-2009] $)
  $}

  ${
    qorim12i.1 $e |- ( ph -> ps ) $.
    qorim12i.2 $e |- ( ch -> th ) $.
    $( Join both sides with disjunction. $)
    qorim12i $p |- ( ( ph \/ ch ) -> ( ps \/ th ) ) $=
      ( wo qorim2i qorim1i qsyl ) ACGADGBDGCDAFHABDEIJ $.
      $( [10-Sep-2009] $) $( [5-Aug-2009] $)
  $}

  $( Disjunction in terms of conjunction. $)
  qorana $p |- ( ( ph \/ ps ) -> -. ( -. ph /\ -. ps ) ) $=
    ( wo wn wa qnotnot1 qorim12i df-qana qcon2i qsyl ) ABCADZDZBDZDZCZKMEZDALBN
    AFBFGPOKMHIJ $.
    $( [10-Sep-2009] $) $( [5-Aug-2009] $)

  $( Disjunction in terms of conjunction. $)
  qoranb $p |- ( -. ( -. ph /\ -. ps ) -> ( ph \/ ps ) ) $=
    ( wn wa wo df-qanb qcon1i qnotnot2 qorim12i qsyl ) ACZBCZDZCKCZLCZEZABEPMKL
    FGNAOBAHBHIJ $.
    $( [10-Sep-2009] $) $( [5-Aug-2009] $)

  $( Negated disjunction in terms of conjunction (DeMorgan's law). $)
  qiorana $p |- ( -. ( ph \/ ps ) -> ( -. ph /\ -. ps ) ) $=
    ( wo wn wa qoranb qcon3i qnotnot2 qsyl ) ABCZDADBDEZDZDKLJABFGKHI $.
    $( [11-Sep-2009] $) $( [5-Aug-2009] $)

  $( Negated disjunction in terms of conjunction (DeMorgan's law). $)
  qioranb $p |- ( ( -. ph /\ -. ps ) -> -. ( ph \/ ps ) ) $=
    ( wn wa wo qnotnot1 qorana qcon3i qsyl ) ACBCDZJCZCABEZCJFLKABGHI $.
    $( [11-Sep-2009] $) $( [5-Aug-2009] $)

  $( Negated conjunction in terms of disjunction (DeMorgan's law). $)
  qianora $p |- ( -. ( ph /\ ps ) -> ( -. ph \/ -. ps ) ) $=
    ( wn wo wa df-qanb qcon1i ) ACBCDABEABFG $.
    $( [11-Sep-2009] $) $( [5-Aug-2009] $)

  $( Negated conjunction in terms of disjunction (DeMorgan's law).

     Note:  see description of quantum logic system Q3 under ~ ax-q1 . $)
  qianorb $p |- ( ( -. ph \/ -. ps ) -> -. ( ph /\ ps ) ) $=
    ( wa wn wo df-qana qcon2i ) ABCADBDEABFG $.
    $( [11-Sep-2009] $) $( [5-Aug-2009] $)

  $( Conjunction in terms of disjunction. $)
  qanor1a $p |- ( ( ph /\ -. ps ) -> -. ( -. ph \/ ps ) ) $=
    ( wn wa wo df-qana qnotnot1 qorim2i qcon3i qsyl ) ABCZDACZKCZEZCLBEZCAKFONB
    MLBGHIJ $.
    $( [12-Sep-2009] $) $( [5-Aug-2009] $)

  $( Conjunction in terms of disjunction. $)
  qanor1b $p |- ( -. ( -. ph \/ ps ) -> ( ph /\ -. ps ) ) $=
    ( wn wo wa qnotnot2 qorim2i qcon3i df-qanb qsyl ) ACZBDZCKBCZCZDZCAMEOLNBKB
    FGHAMIJ $.
    $( [12-Sep-2009] $) $( [5-Aug-2009] $)

  $( Relation between conjunction and disjunction. $)
  qanor2a $p |- ( ( -. ph /\ ps ) -> -. ( ph \/ -. ps ) ) $=
    ( wn wa wo df-qana qnotnot1 qorim1i qcon3i qsyl ) ACZBDKCZBCZEZCAMEZCKBFONA
    LMAGHIJ $.
    $( [12-Sep-2009] $) $( [5-Aug-2009] $)

  $( Relation between conjunction and disjunction. $)
  qanor2b $p |- ( -. ( ph \/ -. ps ) -> ( -. ph /\ ps ) ) $=
    ( wn wo wa qnotnot2 qorim1i qcon3i df-qanb qsyl ) ABCZDZCACZCZKDZCMBEOLNAKA
    FGHMBIJ $.
    $( [12-Sep-2009] $) $( [5-Aug-2009] $)

  $( Absorption law.

     Note:  see description of quantum logic system Q3 under ~ ax-q1 . $)
  qanabsa $p |- ( ( ph \/ ( ph /\ ps ) ) -> ph ) $=
    ( wa wo qorcoma wn wi df-qora ax-q4 qanr qsyl ax-q11 ax-qmp ) AABCZDNADZAAN
    EONFZPAGGZANAHPFZAGZQAGZRNANIABJKZSSTGUAPALMMKK $.
    $( [13-Sep-2009] $) $( [3-Sep-2009] $)

  $( Law of excluded middle. $)
  qexclmid $p |- ( ph \/ -. ph ) $=
    ( wn wi wo qid ax-q3 ax-qmp df-qorb ) ABZIICZCZAIDJKIEIIFGAIHG $.
    $( [14-Sep-2009] $) $( [1-Aug-2009] $)

  $( One direction of Kalmbach's axiom 8. $)
  qa8a $p |- ( ( -. ph /\ ph ) -> ( ( -. ph /\ ph ) /\ ps ) ) $=
    ( wn wa wo df-qana qorcoma qexclmid qa1i qnotnot1 qorim1i qsyl qcon3i
    df-qanb ) ACZADZPCZBCZEZCZPBDPOCZOEZCTOAFSUBSRQEZUBQRGUCAOEZUBUDUCAHIAUAOAJ
    KLLMLPBNL $.
    $( [14-Sep-2009] $) $( [5-Aug-2009] $)

  $( The other direction of Kalmbach's axiom 8.

     Note:  see description of quantum logic system Q3 under ~ ax-q1 . $)
  qa8b $p |- ( ( ( -. ph /\ ph ) /\ ps ) -> ( -. ph /\ ph ) ) $=
    ( wn wa wo df-qana wi qexclmid qorcoma ax-qmp qa1i df-qorb qsyl qianorb
    qorim2i qcon3i df-qanb ) ACZADZBDZRCZREZCZSTSCZBCZEZCUCSBFUBUFUBUEUDEZUFUBU
    EUBEZUGUBUECZUIUBGZGZUHUKUBUJUIUBUIRUAEUBRHRUAIJKKKUEUBLMUBUDUERANOMUEUDIMP
    MRAQM $.
    $( [14-Sep-2009] $) $( [5-Aug-2009] $)

  $( Absorption law. $)
  qorabsa $p |- ( ( ph /\ ( ph \/ ps ) ) -> ph ) $=
    ( wo wa wn wi ax-q1 df-qorb qsyl qorcoma qianorb qcon4i ) AAABCZDZAEZOMEZCZ
    NEOPOCZQOPEZSOFFROSGPOHIPOJIAMKIL $.
    $( [16-Sep-2009] $) $( [5-Aug-2009] $)

  $( Absorption law. $)
  qorabsb $p |- ( ph -> ( ph /\ ( ph \/ ps ) ) ) $=
    ( wn wa wo qanabsa qcon2i qanor1b qoranb qanim2i qsyl ) AACZLBCZDZEZCZAABEZ
    DZOALMFGPANCZDRANHSQAABIJKK $.
    $( [16-Sep-2009] $) $( [8-Aug-2009] $)

  $( Theorem justifying a definition of false (by showing that the expression
     for it is independent of the wff variable). $)
  q0equiv0a $p |- ( ( -. ph /\ ph ) -> ( -. ps /\ ps ) ) $=
    ( wn wa wo qexclmid qorcoma ax-qmp qianorb qa1i qcon4i ) BCBDZACZADZNCZLCMC
    ZMEZOMPEQMFMPGHMAIHJK $.
    $( [16-Sep-2009] $) $( [5-Aug-2009] $)

  $( True is equivalent to true. $)
  q1equiv1a $p |- ( ( ph \/ -. ph ) -> ( ps \/ -. ps ) ) $=
    ( wn wo wi qexclmid ax-q1 ax-qmp ) AACDZIBBCDZEZAFJIKEBFJIGHH $.
    $( [17-Sep-2009] $) $( [5-Aug-2009] $)

  $( Idempotent law. $)
  qanidma $p |- ( ( ph /\ ph ) -> ph ) $=
    ( wa wn wo qiorana qoranb qanim2i qorabsa qsyl qcon4i ) AABAAACZCKBZDZBAAMA
    MAMCKLCZBZKALEOKKADZBKNPKKAFGKAHIIJGALHI $.
    $( [18-Sep-2009] $) $( [5-Aug-2009] $)

  $( Idempotent law. $)
  qanidmb $p |- ( ph -> ( ph /\ ph ) ) $=
    ( wn wa wo qorabsb qorana qanim2i qsyl qioranb qcon4i ) AAAABZBKCZDZCAACALE
    MAAAMKKLBZCZMBKKKADZCOKAEPNKKAFGHALIHJGH $.
    $( [18-Sep-2009] $) $( [5-Aug-2009] $)

  $( Idempotent law. $)
  qoridma $p |- ( ( ph \/ ph ) -> ph ) $=
    ( wo wn wa qorana qanidmb qcon1i qsyl ) AABACZIDZCAAAEAJIFGH $.
    $( [18-Sep-2009] $) $( [5-Aug-2009] $)

  $( Idempotent law. $)
  qoridmb $p |- ( ph -> ( ph \/ ph ) ) $=
    ( wn wa wo qanidma qcon2i qoranb qsyl ) AABZICZBAADJAIEFAAGH $.
    $( [18-Sep-2009] $) $( [5-Aug-2009] $)

  $( Absorption law. $)
  qanabsb $p |- ( ph -> ( ph \/ ( ph /\ ps ) ) ) $=
    ( wn wo wa qorabsa qcon2i qianora qanim2i qcon3i qoranb qsyl ) AACZMBCZDZEZ
    CZAABEZDZPAMNFGQMRCZEZCSUAPTOMABHIJARKLL $.
    $( [23-Sep-2009] $) $( [5-Aug-2009] $)

  $( Associative law. $)
  qorassb $p |-  ( ( ph \/ ( ps \/ ch ) ) -> ( ( ph \/ ps ) \/ ch ) ) $=
    ( wo qorcoma qorassa qsyl ) ABCDZDHADZABDZCDZAHEIBCADZDZKBCAFMCJDZKMLBDNBLE
    CABFGCJEGGG $.
    $( [23-Sep-2009] $) $( [5-Aug-2009] $)

  $( Associative law. $)
  qanassa $p |-  ( ( ( ph /\ ps ) /\ ch ) -> ( ph /\ ( ps /\ ch ) ) ) $=
    ( wa wn wo df-qana qianora qorim2i qorassb qianorb qorim1i qsyl qcon3i
    df-qanb ) ABDZCDPEZCEZFZEZABCDZDZPCGTAEZUAEZFZEUBUESUEUCBEZRFZFZSUDUGUCBCHI
    UHUCUFFZRFSUCUFRJUIQRABKLMMNAUAOMM $.
    $( [23-Sep-2009] $) $( [5-Aug-2009] $)

  $( Associative law. $)
  qanassb $p |-  ( ( ph /\ ( ps /\ ch ) ) -> ( ( ph /\ ps ) /\ ch ) ) $=
    ( wa qancoma qanassa qsyl ) ABCDZDHADZABDZCDZAHEIBCADZDZKBCAFMCJDZKMLBDNBLE
    CABFGCJEGGG $.
    $( [23-Sep-2009] $) $( [5-Aug-2009] $)

  $( Swap disjuncts. $)
  qor12a $p |- ( ( ph \/ ( ps \/ ch ) ) -> ( ps \/ ( ph \/ ch ) ) ) $=
    ( wo qorassb qorcoma qorim1i qsyl qorassa ) ABCDDZBADZCDZBACDDJABDZCDLABCEM
    KCABFGHBACIH $.
    $( [8-Oct-2009] $) $( [5-Aug-2009] $)

  $( Swap conjuncts. $)
  qan12a $p |- ( ( ph /\ ( ps /\ ch ) ) -> ( ps /\ ( ph /\ ch ) ) ) $=
    ( wa qanassb qancoma qanim1i qsyl qanassa ) ABCDDZBADZCDZBACDDJABDZCDLABCEM
    KCABFGHBACIH $.
    $( [8-Oct-2009] $) $( [5-Aug-2009] $)

  $( Swap disjuncts. $)
  qor23a $p |- ( ( ( ph \/ ps ) \/ ch ) -> ( ( ph \/ ch ) \/ ps ) ) $=
    ( wo qorassa qorcoma qorim2i qorassb qsyl ) ABDCDABCDZDZACDBDZABCEKACBDZDLJ
    MABCFGACBHII $.
    $( [11-Oct-2009] $) $( [5-Aug-2009] $)

  $( Swap conjuncts. $)
  qan23a $p |- ( ( ( ph /\ ps ) /\ ch ) -> ( ( ph /\ ch ) /\ ps ) ) $=
    ( wa qanassa qancoma qanim2i qanassb qsyl ) ABDCDABCDZDZACDBDZABCEKACBDZDLJ
    MABCFGACBHII $.
    $( [11-Oct-2009] $) $( [5-Aug-2009] $)

  $( Swap disjuncts. $)
  qor4a $p |- ( ( ( ph \/ ps ) \/ ( ch \/ th ) )
           -> ( ( ph \/ ch ) \/ ( ps \/ th ) ) ) $=
    ( wo qorassa qor12a qorim2i qorassb qsyl ) ABECDEZEABKEZEZACEBDEZEZABKFMACN
    EZEOLPABCDGHACNIJJ $.
    $( [13-Oct-2009] $) $( [5-Aug-2009] $)

  $( Swap conjuncts. $)
  qan4a $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) )
            -> ( ( ph /\ ch ) /\ ( ps /\ th ) ) ) $=
    ( wa qanassa qan12a qanim2i qanassb qsyl ) ABECDEZEABKEZEZACEBDEZEZABKFMACN
    EZEOLPABCDGHACNIJJ $.
    $( [13-Oct-2009] $) $( [5-Aug-2009] $)

  $( Discard a conjunct in the antecedent.

     Note:  see description of quantum logic system Q3 under ~ ax-q1 . $)
  qleorana $p |- ( ( ph /\ ps ) -> ph ) $=
    ( qanr ) ABC $.
    $( [13-Oct-2009] $) $( [5-Aug-2009] $)

$( (End of Norm Megill's mathbox.) $)


