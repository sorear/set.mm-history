$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
       Star rings through Hilbert spaces using extensible structure builders
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( $c C_rng $. $)
  $( Extend class notation subring. $)
  $( csrng $a class C_rng $. $)

  ${
    $d r s x y $.
    $( Def. 8.5.8, p. 216. $)
    $( Define ` s ` is a subring of ` r ` . $)
    $(
    df-subring $a |- C_rng = { <. s , r >. | ( ( base ` s ) C_ ( base ` r )
        /\ A. x e. ( base ` s ) A. y e. ( base ` s ) ( ( x ( +g ` s ) y )
  = ( x ( +g ` r ) y ) /\ ( x ( .r ` s ) y ) = ( x ( .r ` r ) y ) ) ) } $.
    $)
  $}

  $c *v $.
  $c Ring* $.

  $( Extend class notation with involution. $)
  cstv $a class *v $.

  $( Extend class notation with class of all *-rings. $)
  csr $a class Ring* $.

  $( Def. 8.5.23, p. 225. $)
  $( Define involution (the star operation of a *-ring). $)
  df-starv $a |- *v = ( v e. _V |-> ( v ` 4 ) ) $.

  ${
    $d f i k p q r t $.
    $( Def. 8.5.6 p. 216 $)
    $( Define class of all star rings.  A star ring is a ring with an
       involution operation. $)
    df-starrng $a |- Ring* = Struct ( 4 , f , ( f e. RingNEW /\
       E. k E. p E. t E. i ( ( k = ( base ` f ) /\ p = ( +g ` f ) )
          /\ ( t = ( .r ` f ) /\ i = ( *v ` f ) ) /\
         A. q e. k A. r e. k ( ( i ` q ) e. k
              /\ ( ( i ` ( q p r ) ) = ( ( i ` q ) p ( i ` r ) )
              /\ ( i ` ( q t r ) ) = ( ( i ` r ) t ( i ` q ) )
              /\ ( i ` ( i ` q ) ) = q ) ) ) ) ) $.
  $}

  ${
    $d f i k p q r t I $.  $d f i k p q r t K $.  $d f i k p q r t P $.
    $d f i k p q r t R $.  $d f i k p q r t T $.
    issrng.1 $e |- S = Struct ( 4 , f , T. ) $.
    issrng.k $e |- K = ( base ` R ) $.
    issrng.p $e |- P = ( +g ` R ) $.
    issrng.t $e |- T = ( .r ` R ) $.
    issrng.i $e |- I = ( *v ` R ) $.
    $( The predicate "is a star ring." $)
    issrng $p |- ( R e. Ring* <-> ( R e. S /\ R e. RingNEW
        /\ A. q e. K A. r e. K ( ( I ` q ) e. K
              /\ ( ( I ` ( q P r ) ) = ( ( I ` q ) P ( I ` r ) )
              /\ ( I ` ( q T r ) ) = ( ( I ` r ) T ( I ` q ) )
              /\ ( I ` ( I ` q ) ) = q ) ) ) ) $=
      ( vk vp vt vi csr wcel cv crg cbs cfv wceq cplusg wa cmulr cstv co w3a
      wral wex c4 cstru df-starrng eleq2i eleq1 fveq2 syl6eqr eqeq2d anbi12d
      3anbi12d 4exbidv elstr2 3anass cvv fvex eqeltri eleq2 anbi1d raleqbi1dv
      opreq fveq2d eqeq12d 3anbi1d anbi2d 2ralbidv 3anbi2d fveq1 eleq1d
      opreq12d id fveq12d eqeq1d 3anbi123d ceqsex4v 3anbi3i bitr3i 3bitri ) BST
      BEUAZUBTZOUAZWKUCUDZUEZPUAZWKUFUDZUEZUGZQUAZWKUHUDZUEZRUAZWKUIUDZUEZUGZIU
      AZXCUDZWMTZXGHUAZWPUJZXCUDZXHXJXCUDZWPUJZUEZXGXJWTUJZXCUDZXMXHWTUJZUEZXHX
      CUDZXGUEZUKZUGZHWMULZIWMULZUKZRUMQUMPUMOUMZUGZEUNUOZTBCTZBUBTZWMGUEZWPAUE
      ZUGZWTDUEZXCFUEZUGZYEUKZRUMQUMPUMOUMZUGZUGZYJYKXGFUDZGTZXGXJAUJZFUDZUUBXJ
      FUDZAUJZUEZXGXJDUJZFUDZUUFUUBDUJZUEZUUBFUDZXGUEZUKZUGZHGULIGULZUKZSYIBQER
      OHIPUPUQYHYTCEBUNJWKBUEZWLYKYGYSWKBUBURUUSYFYROPQRUUSWSYNXFYQYEUUSWOYLWRY
      MUUSWNGWMUUSWNBUCUDZGWKBUCUSKUTVAUUSWQAWPUUSWQBUFUDZAWKBUFUSLUTVAVBUUSXBY
      OXEYPUUSXADWTUUSXABUHUDZDWKBUHUSMUTVAUUSXDFXCUUSXDBUIUDZFWKBUIUSNUTVAVBVC
      VDVBVEUUAYJYKYSUKUURYJYKYSVFYSUUQYJYKYEXHGTZYBUGZHGULZIGULUVDUUDXCUDZXHXM
      AUJZUEZXSYAUKZUGZHGULIGULUVDUVIUUIXCUDZXMXHDUJZUEZYAUKZUGZHGULIGULUUQOPQR
      GADFGUUTVGKBUCVHVIAUVAVGLBUFVHVIDUVBVGMBUHVHVIFUVCVGNBUIVHVIYDUVFIWMGYCUV
      EHWMGYLXIUVDYBWMGXHVJVKVLVLYMUVEUVKIHGGYMYBUVJUVDYMXOUVIXSYAYMXLUVGXNUVHY
      MXKUUDXCXGXJWPAVMVNXHXMWPAVMVOVPVQVRYOUVKUVPIHGGYOUVJUVOUVDYOXSUVNUVIYAYO
      XQUVLXRUVMYOXPUUIXCXGXJWTDVMVNXMXHWTDVMVOVSVQVRYPUVPUUPIHGGYPUVDUUCUVOUUO
      YPXHUUBGXGXCFVTZWAYPUVIUUHUVNUULYAUUNYPUVGUUEUVHUUGUUDXCFVTYPXHUUBXMUUFAU
      VQXJXCFVTZWBVOYPUVLUUJUVMUUKUUIXCFVTYPXMUUFXHUUBDUVRUVQWBVOYPXTUUMXGYPXHU
      UBXCFYPWCUVQWDWEWFVBVRWGWHWIWJ $.
      $( [22-Sep-2011] $)
  $}

  $c DivRing* $.

  $( Extend class notation with class of all *-division rings. $)
  csd $a class DivRing* $.

  $( Define the class notation with class of all *-division rings, which are
     division rings that are also *-rings. $)
  df-sdivrng $a |- DivRing* = ( DivRing i^i Ring* ) $.

  $( The predicate "is a *-division ring." $)
  issdrng $p |- ( R e. DivRing* <-> ( R e. DivRing /\ R e. Ring* ) ) $=
    ( csd wcel cdrng csr cin wa df-sdivrng eleq2i elin bitri ) ABCADEFZCADCAECG
    BLAHIADEJK $.
    $( [22-Sep-2011] $)

  $c vbase $.
  $c vadd $.
  $c vsca $.
  $c LVec $.

  $( Extend class notation with set of vectors in a left vector space. $)
  cvbase $a class vbase $.

  $( Extend class notation with vector addition. $)
  cvadd $a class vadd $.

  $( Extend class notation with scalar product. $)
  cvsca $a class vsca $.

  $( Extend class notation with class of all left vector spaces. $)
  clvec $a class LVec $.

  $( Def. 8.5.12, p. 219. $)
  $( Define set of vectors in a vector space ` v ` . $)
  df-vbase $a |- vbase = ( v e. _V |-> ( v ` 5 ) ) $.

  $( Def. 8.5.12, p. 219. $)
  $( Define vector addition. $)
  df-vadd $a |- vadd = ( v e. _V |-> ( v ` 6 ) ) $.

  $( Def. 8.5.12, p. 219. $)
  $( Define scalar product. $)
  df-vsca $a |- vsca = ( v e. _V |-> ( v ` 7 ) ) $.

  ${
    $d a f k p q r s t v w x $.
    $( Def. 8.5.11, p. 218. $)
    $( Define class of all left vector spaces (over a *-division ring). $)
    df-lvec $a |- LVec = Struct ( 7 , f , ( f e. DivRing* /\
      E. k E. p E. t E. v E. a E. s
         ( ( k = ( base ` f ) /\ p = ( +g ` f ) /\ t = ( .r ` f ) )
          /\ ( v = ( vbase ` f ) /\ a = ( vadd ` f ) /\ s = ( vsca ` f ) )
          /\ ( { <. 1 , v >. , <. 2 , a >. } e. AbelNEW
             /\ A. q e. k A. r e. k A. x e. v A. w e. v
               ( ( ( r s w ) e. v
                 /\ ( r s ( w a x ) ) = ( ( r s w ) a ( r s x ) )
                 /\ ( ( q p r ) s w ) = ( ( q s w ) a ( r s w ) )
               ) /\ ( ( q s ( r s w ) ) = ( ( q t r ) s w )
                 /\ ( ( 1rNEW ` f ) s w ) = w ) ) ) ) ) ) $.
  $}

  ${
    $d a f k p q r s t v w x A $.  $d a s G $.  $d a f k p q r s t v K $.
    $d a f k p q r s t v w x P $.  $d a f k p q r s t v w x S $.
    $d a f k p q r s t v w x T $.  $d a f k p s t v U $.
    $d a f k p q r s t v w x V $.  $d a f k p q r s t v w x W $.
    islvec.1 $e |- Q = Struct ( 7 , f , T. ) $.
    islvec.k $e |- K = ( base ` W ) $.
    islvec.p $e |- P = ( +g ` W ) $.
    islvec.t $e |- T = ( .r ` W ) $.
    islvec.v $e |- V = ( vbase ` W ) $.
    islvec.a $e |- A = ( vadd ` W ) $.
    islvec.s $e |- S = ( vsca ` W ) $.
    islvec.u $e |- U = ( 1rNEW ` W ) $.
    islvec.9 $e |- G = { <. 1 , V >. , <. 2 , A >. } $.
    $( The predicate "is a left vector space" (over a *-division ring) $)
    islvec $p |- ( W e. LVec <-> ( W e. Q /\ ( W e. DivRing* /\ G e. AbelNEW
        /\ A. q e. K A. r e. K A. x e. V A. w e. V
               ( ( ( r S w ) e. V
                 /\ ( r S ( w A x ) ) = ( ( r S w ) A ( r S x ) )
                 /\ ( ( q P r ) S w ) = ( ( q S w ) A ( r S w ) )
               ) /\ ( ( q S ( r S w ) ) = ( ( q T r ) S w )
                 /\ ( U S w ) = w ) ) ) ) ) $=
      ( vk vp vt vv va vs clvec wcel cv csd cbs cfv wceq cplusg cmulr w3a
      cvbase cvadd cvsca c1 cop c2 cpr cabel co cur wa wral wex c7 cstru
      df-lvec eleq2i eleq1 fveq2 syl6eqr eqeq2d 3anbi123d opreq1d eqeq1d
      anbi2d 2ralbidv 3exbidv anbi12d elstr2 cvv fvex eqeltri id raleq
      raleqbidv biidd opreq anbi1d opeq2 preq1 syl eleq1d eleq2 preq2 opreq2d
      eqeq12d opreq12d eqidd opreq123d ceqsex6v anbi2i 3anass bicomi bitri
      3bitri ) MUKULMIUMZUNULZUEUMZXPUOUPZUQZUFUMZXPURUPZUQZUGUMZXPUSUPZUQZUTZU
      HUMZXPVAUPZUQZUIUMZXPVBUPZUQZUJUMZXPVCUPZUQZUTZVDYHVEZVFYKVEZVGZVHULZNUMZ
      BUMZYNVIZYHULZUUBUUCAUMZYKVIZYNVIZUUDUUBUUFYNVIZYKVIZUQZOUMZUUBYAVIZUUCYN
      VIZUULUUCYNVIZUUDYKVIZUQZUTZUULUUDYNVIZUULUUBYDVIZUUCYNVIZUQZXPVJUPZUUCYN
      VIZUUCUQZVKZVKZBYHVLAYHVLZNXRVLOXRVLZVKZUTZUJVMUIVMUHVMZUGVMUFVMUEVMZVKZI
      VNVOZULMEULZMUNULZXRKUQZYADUQZYDGUQZUTZYHLUQZYKCUQZYNFUQZUTZUUAUURUVBHUUC
      YNVIZUUCUQZVKZVKZBYHVLAYHVLZNXRVLZOXRVLZVKZUTZUJVMUIVMUHVMZUGVMUFVMUEVMZV
      KZVKUVPUVQJVHULZUUBUUCFVIZLULZUUBUUCUUFCVIZFVIZUWSUUBUUFFVIZCVIZUQZUULUUB
      DVIZUUCFVIZUULUUCFVIZUWSCVIZUQZUTZUULUWSFVIZUULUUBGVIZUUCFVIZUQZHUUCFVIZU
      UCUQZVKZVKZBLVLALVLZNKVLOKVLZUTZVKUKUVOMABUHUGIUEUJNOUFUIVPVQUVNUWQEIMVNP
      XPMUQZXQUVQUVMUWPXPMUNVRUYCUVLUWOUEUFUGUYCUVKUWNUHUIUJUYCYGUWAYQUWEUVJUWM
      UYCXTUVRYCUVSYFUVTUYCXSKXRUYCXSMUOUPZKXPMUOVSQVTWAUYCYBDYAUYCYBMURUPZDXPM
      URVSRVTWAUYCYEGYDUYCYEMUSUPZGXPMUSVSSVTWAWBUYCYJUWBYMUWCYPUWDUYCYILYHUYCY
      IMVAUPZLXPMVAVSTVTWAUYCYLCYKUYCYLMVBUPZCXPMVBVSUAVTWAUYCYOFYNUYCYOMVCUPZF
      XPMVCVSUBVTWAWBUYCUVIUWLUUAUYCUVHUWJONXRXRUYCUVGUWIABYHYHUYCUVFUWHUURUYCU
      VEUWGUVBUYCUVDUWFUUCUYCUVCHUUCYNUYCUVCMVJUPHXPMVJVSUCVTWCWDWEWEWFWFWEWBWG
      WGWHWIUWQUYBUVPUWQUVQUWRUYAVKZVKZUYBUWPUYJUVQUWMUUAUWJNKVLZOKVLZVKUUAUUEU
      UKUXFUUCYNVIZUUPUQZUTZUWHVKZBYHVLAYHVLZNKVLOKVLZVKUUAUYPUUSUXMUUCYNVIZUQZ
      UWGVKZVKZBYHVLZAYHVLZNKVLOKVLZVKVDLVEZYSVGZVHULZUUDLULZUUKUYOUTZVUBVKZBLV
      LZALVLZNKVLOKVLZVKUWRVUJUUBUXAYNVIZUUDUUICVIZUQZUYNUUOUUDCVIZUQZUTZVUBVKZ
      BLVLALVLZNKVLOKVLZVKUYJUEUFUGUHUIUJKDGLCFKUYDWJQMUOWKWLDUYEWJRMURWKWLGUYF
      WJSMUSWKWLLUYGWJTMVAWKWLCUYHWJUAMVBWKWLFUYIWJUBMVCWKWLUVRUWLUYMUUAUVRUWKU
      YLOXRKUVRWMUWJNXRKWNWOWEUVSUYMUYSUUAUVSUWJUYRONKKUVSUWIUYQABYHYHUVSUURUYP
      UWHUVSUUEUUEUUKUUKUUQUYOUVSUUEWPUVSUUKWPUVSUUNUYNUUPUVSUUMUXFUUCYNUULUUBY
      ADWQWCWDWBWRWFWFWEUVTUYSVUFUUAUVTUYRVUEONKKUVTUYQVUCABYHYHUVTUWHVUBUYPUVT
      UVBVUAUWGUVTUVAUYTUUSUVTUUTUXMUUCYNUULUUBYDGWQWCWAWRWEWFWFWEUWBUUAVUIVUFV
      UOUWBYTVUHVHUWBYRVUGUQYTVUHUQYHLVDWSYRVUGYSWTXAXBUWBVUEVUNONKKUWBVUDVUMAY
      HLUWBWMZUWBVUCVULBYHLVVEUWBUYPVUKVUBUWBUUEVUJUUKUUKUYOUYOYHLUUDXCUWBUUKWP
      UWBUYOWPWBWRWOWOWFWHUWCVUIUWRVUOVVDUWCVUHJVHUWCVUHVUGVFCVEZVGZJUWCYSVVFUQ
      VUHVVGUQYKCVFWSYSVVFVUGXDXAUDVTXBUWCVUNVVCONKKUWCVULVVBABLLUWCVUKVVAVUBUW
      CVUJVUJUUKVURUYOVUTUWCVUJWPUWCUUHVUPUUJVUQUWCUUGUXAUUBYNUUCUUFYKCWQXEUUDU
      UIYKCWQXFUWCUUPVUSUYNUUOUUDYKCWQWAWBWRWFWFWHUWDVVDUYAUWRUWDVVCUXTONKKUWDV
      VBUXSABLLUWDVVAUXKVUBUXRUWDVUJUWTVURUXEVUTUXJUWDUUDUWSLUUBUUCYNFWQZXBUWDV
      UPUXBVUQUXDUUBUXAYNFWQUWDUUDUWSUUIUXCCVVHUUBUUFYNFWQXGXFUWDUYNUXGVUSUXIUX
      FUUCYNFWQUWDUUOUXHUUDUWSCUULUUCYNFWQVVHXGXFWBUWDVUAUXOUWGUXQUWDUUSUXLUYTU
      XNUWDUULUULUUDUWSYNFUWDWMUWDUULXHVVHXIUXMUUCYNFWQXFUWDUWFUXPUUCHUUCYNFWQW
      DWHWHWFWFWEXJXKUYBUYKUVQUWRUYAXLXMXNXKXO $.
      $( [21-Sep-2011] $)
  $}

  $c 0vNEW $.

  $( Extend class notation with zero vector. $)
  czv $a class 0vNEW $.

  $( Def. 8.5.13, p. 219. $)
  $( Define zero vector. $)
  df-zv $a |- 0vNEW = ( v e. _V |-> ( 0g `
           { <. 1 , ( vbase ` v ) >. , <. 2 , ( vadd ` v ) >. } ) ) $.

  $c ip $.
  $c PreHil $.

  $( Extend class notation with Hermitian form (inner product). $)
  cipr $a class ip $.

  $( Extend class notation with class all pre-Hilbert spaces. $)
  cprehil $a class PreHil $.

  $( Def. 8.5.25, p. 226. $)
  $( Define Hermitian form (inner product). $)
  df-inprod $a |- ip = ( v e. _V |-> ( v ` 8 ) ) $.

  ${
    $d a f h i k p q r s t v w x y z $.
    $( Def. 8.5.24, p. 225. $)
    $( Define class all generalized pre-Hilbert spaces (over *-division
       ring). $)
    df-prehil $a |- PreHil = Struct ( 8 , f , ( f e. LVec /\
       E. k E. p E. t E. i E. v E. a E. s E. h (
        ( ( k = ( base ` f ) /\ p = ( +g ` f ) )
              /\ ( t = ( .r ` f ) /\ i = ( *v ` f ) ) )
        /\ ( ( v = ( vbase ` f ) /\ a = ( vadd ` f ) )
              /\ ( s = ( vsca ` f ) /\ h = ( ip ` f ) ) )
          /\ A. q e. k A. x e. v A. y e. v A. z e. v (
            ( x h y ) e. k
              /\ ( ( ( ( q s x ) a y ) h z )
                  = ( ( q t ( x h z ) ) p ( y h z ) )
        /\ ( ( x h x ) = ( 0g ` f ) -> x = ( 0vNEW ` f ) )
              /\ ( i ` ( x h y ) ) = ( y h x ) ) ) ) ) ) $.
  $}

  ${
    $d a f h i k p q s t v x y z A $.  $d a f h i k p q s t v x y z H $.
    $d a f h i k p q s t v x y z I $.  $d a f h i k p q s t v x y z K $.
    $d a f h i k p s t v N $.  $d a f h i k p q s t v x y z P $.
    $d a f h i k p q s t v x y z S $.  $d a f h i k p q s t v x y z T $.
    $d a f h i k p q s t v x y z V $.  $d a f h i k p q s t v x y z W $.
    $d a f h i k p s t v Z $.
    isphil.1 $e |- Q = Struct ( 8 , f , T. ) $.
    isphil.k $e |- K = ( base ` W ) $.
    isphil.p $e |- P = ( +g ` W ) $.
    isphil.t $e |- T = ( .r ` W ) $.
    isphil.i $e |- I = ( *v ` W ) $.
    isphil.v $e |- V = ( vbase ` W ) $.
    isphil.a $e |- A = ( vadd ` W ) $.
    isphil.s $e |- S = ( vsca ` W ) $.
    isphil.h $e |- H = ( ip ` W ) $.
    isphil.10 $e |- N = ( 0g ` W ) $.
    isphil.11 $e |- Z = ( 0vNEW ` W ) $.
    $( The predicate "is a pre-Hilbert space" (over a *-division ring) $)
    isphil $p |- ( W e. PreHil <-> ( W e. Q /\ W e. LVec
          /\ A. q e. K A. x e. V A. y e. V A. z e. V (
             ( x H y ) e. K
              /\ ( ( ( ( q S x ) A y ) H z )
                  = ( ( q T ( x H z ) ) P ( y H z ) )
         /\ ( ( x H x ) = N -> x = Z )
              /\ ( I ` ( x H y ) ) = ( y H x ) ) ) ) ) $=
      ( vk vp vt vi vv va vs vh cprehil wcel cv clvec cbs cfv wceq cplusg wa
      cmulr cstv cvbase cvadd cvsca cipr co c0g czv wi w3a wral wex c8 cstru
      df-prehil eleq2i eleq1 fveq2 syl6eqr eqeq2d anbi12d imbi12d 3anbi2d
      anbi2d 2ralbidv 3anbi123d 4exbidv elstr2 3anass cvv fvex eqeltri eleq2
      anbi1d ralbidv raleqbi1dv opreq 3anbi1d opreq1d fveq1 eqeq1d 3anbi3d
      raleq eleq1d opreq2d opreq12d eqeq12d imbi1d fveq2d ceqsex8v 3anbi3i
      bitr3i 3bitri ) OUQUROIUSZUTURZUIUSZXTVAVBZVCZUJUSZXTVDVBZVCZVEZUKUSZXTVF
      VBZVCZULUSZXTVGVBZVCZVEZVEZUMUSZXTVHVBZVCZUNUSZXTVIVBZVCZVEZUOUSZXTVJVBZV
      CZUPUSZXTVKVBZVCZVEZVEZAUSZBUSZUUGVLZYBURZQUSZUULUUDVLZUUMYTVLZCUSZUUGVLZ
      UUPUULUUSUUGVLZYIVLZUUMUUSUUGVLZYEVLZVCZUULUULUUGVLZXTVMVBZVCZUULXTVNVBZV
      CZVOZUUNYLVBZUUMUULUUGVLZVCZVPZVEZCYQVQBYQVQZAYQVQQYBVQZVPZUPVRUOVRUNVRUM
      VRZULVRUKVRUJVRUIVRZVEZIVSVTZUROFURZOUTURZYBLVCZYEEVCZVEZYIHVCZYLKVCZVEZV
      EZYQNVCZYTDVCZVEZUUDGVCZUUGJVCZVEZVEZUUOUVEUVFMVCZUULPVCZVOZUVNVPZVEZCYQV
      QBYQVQZAYQVQZQYBVQZVPZUPVRUOVRUNVRUMVRZULVRUKVRUJVRUIVRZVEZVEZUWDUWEUULUU
      MJVLZLURZUUPUULGVLZUUMDVLZUUSJVLZUUPUULUUSJVLZHVLZUUMUUSJVLZEVLZVCZUULUUL
      JVLZMVCZUXAVOZUXMKVBZUUMUULJVLZVCZVPZVEZCNVQBNVQZANVQQLVQZVPZUQUWCOABCUMU
      KIUPULUIUOQUJUNWAWBUWBUXKFIOVSRXTOVCZYAUWEUWAUXJXTOUTWCUYNUVTUXIUIUJUKULU
      YNUVSUXHUMUNUOUPUYNYPUWLUUKUWSUVRUXGUYNYHUWHYOUWKUYNYDUWFYGUWGUYNYCLYBUYN
      YCOVAVBZLXTOVAWDSWEWFUYNYFEYEUYNYFOVDVBZEXTOVDWDTWEWFWGUYNYKUWIYNUWJUYNYJ
      HYIUYNYJOVFVBZHXTOVFWDUAWEWFUYNYMKYLUYNYMOVGVBZKXTOVGWDUBWEWFWGWGUYNUUCUW
      OUUJUWRUYNYSUWMUUBUWNUYNYRNYQUYNYROVHVBZNXTOVHWDUCWEWFUYNUUADYTUYNUUAOVIV
      BZDXTOVIWDUDWEWFWGUYNUUFUWPUUIUWQUYNUUEGUUDUYNUUEOVJVBZGXTOVJWDUEWEWFUYNU
      UHJUUGUYNUUHOVKVBZJXTOVKWDUFWEWFWGWGUYNUVQUXEQAYBYQUYNUVPUXDBCYQYQUYNUVOU
      XCUUOUYNUVKUXBUVEUVNUYNUVHUWTUVJUXAUYNUVGMUVFUYNUVGOVMVBMXTOVMWDUGWEWFUYN
      UVIPUULUYNUVIOVNVBPXTOVNWDUHWEWFWHWIWJWKWKWLWMWMWGWNUXLUWDUWEUXJVPUYMUWDU
      WEUXJWOUXJUYLUWDUWEUXGUUNLURZUXCVEZCYQVQBYQVQZAYQVQZQLVQVUCUUTUVBUVCEVLZV
      CZUXBUVNVPZVEZCYQVQBYQVQZAYQVQQLVQVUCUUTUUPUVAHVLZUVCEVLZVCZUXBUVNVPZVEZC
      YQVQBYQVQZAYQVQQLVQVUCVUNUXBUUNKVBZUVMVCZVPZVEZCYQVQZBYQVQZAYQVQZQLVQVVAC
      NVQZBNVQZANVQZQLVQVUCUUQUUMDVLZUUSUUGVLZVUMVCZUXBVUSVPZVEZCNVQBNVQZANVQQL
      VQVUCUXPUUSUUGVLZVUMVCZUXBVUSVPZVEZCNVQBNVQZANVQQLVQUYLUIUJUKULUMUNUOLEHK
      NDGJUPLUYOWPSOVAWQWREUYPWPTOVDWQWRHUYQWPUAOVFWQWRKUYRWPUBOVGWQWRNUYSWPUCO
      VHWQWRDUYTWPUDOVIWQWRGVUAWPUEOVJWQWRJVUBWPUFOVKWQWRUXFVUFQYBLUWFUXEVUEAYQ
      UWFUXDVUDBCYQYQUWFUUOVUCUXCYBLUUNWSWTWKXAXBUWGVUEVUKQALYQUWGVUDVUJBCYQYQU
      WGUXCVUIVUCUWGUVEVUHUXBUVNUWGUVDVUGUUTUVBUVCYEEXCWFXDWJWKWKUWIVUKVUQQALYQ
      UWIVUJVUPBCYQYQUWIVUIVUOVUCUWIVUHVUNUXBUVNUWIVUGVUMUUTUWIUVBVULUVCEUUPUVA
      YIHXCXEWFXDWJWKWKUWJVUQVVCQALYQUWJVUPVVABCYQYQUWJVUOVUTVUCUWJUVNVUSVUNUXB
      UWJUVLVURUVMUUNYLKXFXGXHWJWKWKUWMVVDVVGQLVVCVVFAYQNVVBVVEBYQNVVACYQNXIXBX
      BXAUWNVVFVVMQALNUWNVVAVVLBCNNUWNVUTVVKVUCUWNVUNVVJUXBVUSUWNUUTVVIVUMUWNUU
      RVVHUUSUUGUUQUUMYTDXCXEXGXDWJWKWKUWPVVMVVRQALNUWPVVLVVQBCNNUWPVVKVVPVUCUW
      PVVJVVOUXBVUSUWPVVIVVNVUMUWPVVHUXPUUSUUGUWPUUQUXOUUMDUUPUULUUDGXCXEXEXGXD
      WJWKWKUWQVVRUYKQALNUWQVVQUYJBCNNUWQVUCUXNVVPUYIUWQUUNUXMLUULUUMUUGJXCZXJU
      WQVVOUYBUXBUYEVUSUYHUWQVVNUXQVUMUYAUXPUUSUUGJXCUWQVULUXSUVCUXTEUWQUVAUXRU
      UPHUULUUSUUGJXCXKUUMUUSUUGJXCXLXMUWQUWTUYDUXAUWQUVFUYCMUULUULUUGJXCXGXNUW
      QVURUYFUVMUYGUWQUUNUXMKVVSXOUUMUULUUGJXCXMWLWGWKWKXPXQXRXS $.
      $( [22-Sep-2011] $)
  $}

  $c +ss $.
  $c ocv $.
  $c CSubSp $.
  $c Hil $.

  $( Extend class notation with subspace sum. $)
  cplusss $a class +ss $.

  $( Extend class notation with orthocomplement of a subspace. $)
  cocv $a class ocv $.

  $( Extend class notation with set of closed subspaces. $)
  ccsubsp $a class CSubSp $.

  $( Extend class notation with class of all Hilbert spaces. $)
  chs $a class Hil $.

  ${
    $d h s t u v w x y $.
    $( Define subspace sum. $)
    df-plusss $a |- +ss = ( v e. _V |-> ( x e. ~P ( vbase ` v ) ,
        y e. ~P ( vbase ` v ) |-> { w e. ( vbase ` v )
            | E. t e. x E. u e. y w = ( t ( vadd ` v ) u ) } ) ) $.

    $( Def. 8.5.27, p. 227. $)
    $( Define orthocomplement of a subspace. $)
    df-ocv $a |- ocv = ( h e. _V |-> ( s e. ~P ( vbase ` h ) |->
   { x e. ( vbase ` h ) | A. y e. s ( x ( ip ` h ) y ) = ( 0vNEW ` h ) } ) ) $.

    $( Def. 8.5.28, p. 226. $)
    $( Define set of closed subspaces. $)
    df-csubsp $a |- CSubSp = ( h e. _V |-> { s
   | ( s C_ ( vbase ` h ) /\ s = ( ( ocv ` h ) ` ( ( ocv ` h ) ` s ) ) ) } ) $.

    $( Def. 8.5.30, p. 228. $)
    $( Define class of all Hilbert spaces.  Based on Proposition 4.5, p. 176,
       Gudrun Kalmbach, Quantum Measures and Spaces, Kluwer, Dordrecht,
       1998. $)
    df-hil $a |- Hil = { h e. PreHil | A. s e. ( CSubSp ` h )
          ( s ( +ss ` h ) ( ( ocv ` h ) ` s ) ) = ( vbase ` h ) } $.
  $}

  ${
    $d h A $.  $d h t u w x y H $.  $d h w x y V $.
    plusssfval.v $e |- V = ( vbase ` H ) $.
    plusssfval.a $e |- A = ( vadd ` H ) $.
    plusssfval.s $e |- S = ( +ss ` H ) $.
    $( The subspace sum operation. $)
    plusssfval $p |- ( H e. B -> S = ( x e. ~P V , y e. ~P V |-> { w e. V
            | E. t e. x E. u e. y w = ( t A u ) } ) ) $=
      ( vh wcel cvv cpw cv co wceq wrex crab cmpt2 elisset cplusss cfv cvbase
      cvadd fveq2 syl6eqr pweq syl opreqd eqeq2d 2rexbidv rabeqbidv
      mpt2eq123dv df-plusss fvex eqeltri pwex mpt2exg mp2an fvmpt syl5eq ) IGOI
      POZHABJQZVGCRZERZDRZFSZTZDBRZUAEARZUAZCJUBZUCZTIGUDVFIUEUFVQHNIABNRZUGUFZ
      QZVTVHVIVJVRUHUFZSZTZDVMUAEVNUAZCVSUBZUCVQPUEVRITZABVTVTWEVGVGVPWFVSJTVTV
      GTWFVSIUGUFZJVRIUGUIKUJZVSJUKULZWIWFWDVOCVSJWHWFWCVLEDVNVMWFWBVKVHWFWAFVI
      VJWFWAIUHUFFVRIUHUILUJUMUNUOUPUQABCNDEURVGPOZWJVQPOJJWGPKIUGUSUTVAZWKABVG
      VGVPPPVBVCVDMVEUL $.
      $( [7-Oct-2011] $)

    $d x y A $.  $d t w x y X $.  $d t u w x y Y $.
    $( The subspace sum operation value. $)
    plusssval $p |- ( ( H e. B /\ X C_ V /\ Y C_ V ) -> ( X S Y ) = { w e. V
            | E. t e. X E. u e. Y w = ( t A u ) } ) $=
      ( vx vy wcel wss co cv wceq wrex crab wa cpw cmpt2 plusssfval opreqd
      rexeq rabbidv rexbidv eqid cvbase cfv cvv fvex eqeltri rabex ovmpt2
      elpw2 syl2anbr sylan9eq 3impb ) GEPZIHQZJHQZIJFRZASCSBSDRTZBJUAZCIUAZAHUB
      ZTVCVDVEUCVFIJNOHUDZVKVGBOSZUAZCNSZUAZAHUBZUEZRZVJVCFVQIJNOABCDEFGHKLMUFU
      GIVKPJVKPVRVJTVDVENOIJVKVKVPVJVQVMCIUAZAHUBVNITVOVSAHVMCVNIUHUIVLJTZVSVIA
      HVTVMVHCIVGBVLJUHUJUIVQUKVIAHHGULUMUNKGULUOUPZUQURIHWAUSJHWAUSUTVAVB $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d h s x y H $.  $d h I $.  $d h s x V $.  $d h Z $.
    ocvfval.v $e |- V = ( vbase ` H ) $.
    ocvfval.i $e |- I = ( ip ` H ) $.
    ocvfval.z $e |- Z = ( 0vNEW ` H ) $.
    ocvfval.o $e |- O = ( ocv ` H ) $.
    $( The orthocomplement operation. $)
    ocvfval $p |- ( H e. A -> O = ( s e. ~P V |->
                          { x e. V | A. y e. s ( x I y ) = Z } ) ) $=
      ( vh wcel cvv cpw cv co wceq wral crab cmpt elisset cocv cfv cvbase cipr
      czv fveq2 syl6eqr pweq syl opreqd eqeq12d ralbidv rabeqbidv mpteq12dv
      df-ocv fvex eqeltri pwex mptexg ax-mp fvmpt syl5eq ) DCODPOZFIGQZARZBRZES
      ZHTZBIRZUAZAGUBZUCZTDCUDVGDUEUFVPFNDINRZUGUFZQZVIVJVQUHUFZSZVQUIUFZTZBVMU
      AZAVRUBZUCVPPUEVQDTZIVSWEVHVOWFVRGTVSVHTWFVRDUGUFZGVQDUGUJJUKZVRGULUMWFWD
      VNAVRGWHWFWCVLBVMWFWAVKWBHWFVTEVIVJWFVTDUHUFEVQDUHUJKUKUNWFWBDUIUFHVQDUIU
      JLUKUOUPUQURABNIUSVHPOVPPOGGWGPJDUGUTVAVBIVHVOPVCVDVEMVFUM $.
      $( [7-Oct-2011] $)

    $d s I $.  $d s x y S $.  $d s Z $.
    $( Value of the orthocomplement of a subset (normally a subspace) of a
       pre-Hilbert space. $)
    ocvval $p |- ( ( H e. A /\ S C_ V )
              -> ( O ` S ) = { x e. V | A. y e. S ( x I y ) = Z } ) $=
      ( vs wcel wss cfv cpw cv co wceq wral crab cmpt ocvfval fveq1d cvbase
      cvv fvex eqeltri elpw2 raleq rabbidv eqid rabex fvmpt sylbir sylan9eq )
      ECOZDHPZDGQDNHRZASBSFTIUAZBNSZUBZAHUCZUDZQZVBBDUBZAHUCZUSDGVFABCEFGHINJKL
      MUEUFUTDVAOVGVIUADHHEUGQUHJEUGUIUJZUKNDVEVIVAVFVCDUAVDVHAHVBBVCDULUMVFUNV
      HAHVJUOUPUQUR $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d h s H $.  $d h O $.  $d h s V $.
    csubspset.v $e |- V = ( vbase ` H ) $.
    csubspset.o $e |- O = ( ocv ` H ) $.
    csubspset.c $e |- C = ( CSubSp ` H ) $.
    $( The set of closed subspaces of a pre-Hilbert space. $)
    csubspset $p |- ( H e. A
             -> C = { s | ( s C_ V /\ s = ( O ` ( O ` s ) ) ) } ) $=
      ( vh wcel cvv cv wss cfv wceq wa cab elisset ccsubsp cvbase cocv fveq2
      syl6eqr sseq2d fveq1d fveq12d eqeq2d anbi12d abbidv df-csubsp cpw fvex
      eqeltri pwex visset elpw anbi1i abbii ssab2 eqsstr3i ssexi fvmpt syl5eq
      syl ) CAKCLKZBFMZENZVGVGDOZDOZPZQZFRZPCASVFCTOVMBJCVGJMZUAOZNZVGVGVNUBOZO
      ZVQOZPZQZFRVMLTVNCPZWAVLFWBVPVHVTVKWBVOEVGWBVOCUAOZEVNCUAUCGUDUEWBVSVJVGW
      BVRVIVQDWBVQCUBODVNCUBUCHUDZWBVGVQDWDUFUGUHUIUJJFUKVMEULZEEWCLGCUAUMUNUOV
      MVGWEKZVKQZFRWEWGVLFWFVHVKVGEFUPUQURUSVKFWEUTVAVBVCIVDVE $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d s H $.  $d s O $.  $d s S $.  $d s V $.
    iscsubsp.v $e |- V = ( vbase ` H ) $.
    iscsubsp.o $e |- O = ( ocv ` H ) $.
    iscsubsp.c $e |- C = ( CSubSp ` H ) $.
    $( The predicate "is a closed subspace" (of a pre-Hilbert space). $)
    iscsubsp $p |- ( H e. A
              -> ( S e. C <-> ( S C_ V /\ S = ( O ` ( O ` S ) ) ) ) ) $=
      ( vs wcel cv wss cfv wceq wa cab csubspset eleq2d cvv cvbase fvex
      eqeltri ssex adantr sseq1 id fveq2 fveq2d eqeq12d anbi12d elab3 syl6bb )
      DAKZCBKCJLZFMZUOUOENZENZOZPZJQZKCFMZCCENZENZOZPZUNBVACABDEFJGHIRSUTVFJCVB
      CTKVECFFDUANTGDUAUBUCUDUEUOCOZUPVBUSVEUOCFUFVGUOCURVDVGUGVGUQVCEUOCEUHUIU
      JUKULUM $.
      $( [7-Oct-2011] $)
  $}

  ${
    $d h s C $.  $d h s H $.  $d h O $.  $d h S $.  $d h V $.
    ishil.v $e |- V = ( vbase ` H ) $.
    ishil.s $e |- S = ( +ss ` H ) $.
    ishil.o $e |- O = ( ocv ` H ) $.
    ishil.c $e |- C = ( CSubSp ` H ) $.
    $( The predicate "is a Hilbert space" (over a *-division ring). $)
    ishil $p |- ( H e. Hil
        <-> ( H e. PreHil /\ A. s e. C ( s S ( O ` s ) ) = V ) ) $=
      ( vh cv cocv cfv cplusss co cvbase wceq ccsubsp wral cprehil chs fveq2
      syl6eqr fveq1d opreq2d opreqd eqtrd eqeq12d raleqbidv df-hil elrab2 ) FLZ
      UMKLZMNZNZUNONZPZUNQNZRZFUNSNZTUMUMDNZBPZERZFATKCUAUBUNCRZUTVDFVAAVEVACSN
      AUNCSUCJUDVEURVCUSEVEURUMVBUQPVCVEUPVBUMUQVEUMUODVEUOCMNDUNCMUCIUDUEUFVEU
      QBUMVBVEUQCONBUNCOUCHUDUGUHVEUSCQNEUNCQUCGUDUIUJKFUKUL $.
      $( [7-Oct-2011] $)
  $}

