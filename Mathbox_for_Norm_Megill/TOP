$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                Mathbox for Norm Megill
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  $( Recover the commutative law of addition for complex numbers from the
     Abelian group structure. $)
  cnaddcom $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) = ( B + A ) ) $=
    ( cnx cbs cfv cc cop cplusg caddc cpr cabel wcel co wceq eqid cnaddabl cnex
    cvv grpbase ax-mp addex grpplusg ablcom mp3an1 ) CDEFGCHEIGJZKLAFLBFLABIMBA
    IMNUEUEOZPFIUEABFRLFUEDENQFIUERUFSTIRLIUEHENUAFIUERUFUBTUCUD $.
    $( [17-Mar-2013] $)


  ${
    $d g K $.
    toycom.1 $e |- C = { g e. Abel | ( Base ` g ) = CC } $.
    toycom.2 $e |- O = ( +g ` K ) $.
    $( Show the commutative law for an operation ` O ` on a toy structure class
       ` C ` of commuatitive operations on ` CC ` .  This illustrates how a
       structure class can be partially specialized.  In practice, we would
       ordinarily define a new constant such as "CAbel" in place of ` C ` . $)
    toycom $p |- ( ( K e. C /\ A e. CC /\ B e. CC )
                    -> ( A O B ) = ( B O A ) ) $=
      ( wcel cc w3a cfv co cabel cbs wceq 3ad2ant1 eleqtrrd eqid oveqi cv sseli
      cplusg crab ssrab2 eqsstri simp2 fveq2 eqeq1d elrab2 simprbi simp3 ablcom
      syl3anc 3eqtr4g ) ECIZAJIZBJIZKZABEUCLZMZBAUTMZABFMBAFMUSENIZAEOLZIBVDIVA
      VBPUPUQVCURCNECDUAZOLZJPZDNUDNGVGDNUEUFUBQUSAJVDUPUQURUGUPUQVDJPZURUPVCVH
      VGVHDENCVEEPVFVDJVEEOUHUIGUJUKQZRUSBJVDUPUQURULVIRVDUTEABVDSUTSUMUNFUTABH
      TFUTBAHTUO $.
      $( [17-Mar-2013] $)
  $}

  $( Class variables used as operators, relations, etc. $)

  $v .< $.
$(
  $v .<_ $.
  $v ./\ $.
  $v .\/ $.
  $v .+ $.
$)

  .lt $f class .< $.
$(
  .le $f class .<_ $.
  .an $f class ./\ $.
  .or $f class .\/ $.
  .pl $f class .+ $.
$)

  ${
    $d x y z B $.  $d x y z .\/ $.  $d x y z K $.  $d y z .<_ $.  $d x y z S $.
    $d x y z T $.  $d x y z U $.
    lubunNEW.b $e |- B = ( Base ` K ) $.
    lubunNEW.j $e |- .\/ = ( join ` K ) $.
    lubunNEW.u $e |- U = ( lub ` K ) $.
    $( The LUB of a union. $)
    lubunNEW $p |- ( ( K e. CLat /\ S C_ B /\ T C_ B )
                      -> ( U ` ( S u. T ) ) = ( ( U ` S ) .\/ ( U ` T ) ) ) $=
      ( vy vx vz wcel wbr wral wi wa syl2anc syl3anc adantr ccla wss w3a cun cv
      cfv cple crio co wceq simp1 unss biimpi 3adant1 eqid lubval clat 3ad2ant1
      clatl clatlubcl 3adant3 latjcl wb simpl1 syl simpr latasymb simpl2 sseldd
      3adant2 simpl3 lubel latlej1 lattrd latlej2 ralunb sylanbrc breq2 ralbidv
      ralrimiva imbi12d rcla4v mpid imp ad2ant2rl lubl latjle12 syl13anc sylibd
      anim12d syl5bi adantrr mpbi2and ex wo jaodan sylan2b breq1 imbi2d anbi12d
      elun biimprcd impbid riota5 mpdan eqtrd ) FUAMZBAUBZCAUBZUCZBCUDZDUFZJUEZ
      KUEZFUGUFZNZJXKOZXMLUEZXONZJXKOZXNXRXONZPZLAOZQZKAUHZBDUFZCDUFZEUIZXJXGXK
      AUBZXLYEUJXGXHXIUKXHXIYIXGXHXIQYIBCAULUMUNKJLUAAXKDFXOGXOUOZIUPRXJYHAMZYE
      YHUJXJFUQMZYFAMZYGAMZYKXGXHYLXIFUSZURZXGXHYMXIABDFGIUTZVAZXGXIYNXHACDFGIU
      TZVJZAEFYFYGGHVBZSZXJYDKAYHXJXNAMZYDXNYHUJZVCYKXJUUCQZYDUUDUUEYDUUDUUEYDQ
      XNYHXONZYHXNXONZUUDUUEUUFUUGQUUDVCZYDUUEYLUUCYKUUHUUEXGYLXGXHXIUUCVDZYOVE
      ZXJUUCVFZXJYKUUCUUBTAFXOXNYHGYJVGSTXJYCUUFUUCXQXJYCUUFXJYCXMYHXONZJXKOZUU
      FXJUULJBOUULJCOUUMXJUULJBXJXMBMZQZAFXOXMYFYHGYJUUOXGYLXGXHXIUUNVDZYOVEZUU
      OBAXMXGXHXIUUNVHZXJUUNVFZVIUUOXGXHYMUUPUURYQRZUUOYLYMYNYKUUQUUTUUOXGXIYNU
      UPXGXHXIUUNVKYSRZUUASUUOXGUUNXHXMYFXONUUPUUSUURABDFXOXMGYJIVLSUUOYLYMYNYF
      YHXONUUQUUTUVAAEFXOYFYGGYJHVMSVNZVTXJUULJCXJXMCMZQZAFXOXMYGYHGYJXJYLUVCYP
      TZUVDCAXMXGXHXIUVCVKZXJUVCVFZVIUVDXGXIYNXGXHXIUVCVDZUVFYSRZXJYKUVCUUBTUVD
      XGUVCXIXMYGXONUVHUVGUVFACDFXOXMGYJIVLSUVDYLYMYNYGYHXONUVEUVDXGXHYMUVHXGXH
      XIUVCVHYQRUVIAEFXOYFYGGYJHVOSVNZVTUULJBCVPVQXJYKYCUUMUUFPZPUUBYBUVKLYHAXR
      YHUJZXTUUMYAUUFUVLXSUULJXKXRYHXMXOVRVSXRYHXNXOVRWAWBVEWCWDWEUUEXQUUGYCUUE
      XQUUGXQXPJBOZXPJCOZQZUUEUUGXPJBCVPUUEUVOYFXNXONZYGXNXONZQZUUGUUEUVMUVPUVN
      UVQUUEXGXHUUCUVMUVPPUUIXGXHXIUUCVHUUKJABDFXOXNGYJIWFSUUEXGXIUUCUVNUVQPUUI
      XGXHXIUUCVKUUKJACDFXOXNGYJIWFSWJUUEYLYMYNUUCUVRUUGVCUUJXJYMUUCYRTXJYNUUCY
      TTUUKAEFXOYFYGXNGYJHWGWHWIWKWDWLWMWNXJUUDYDPZUUCXJUUMXTYHXRXONZPZLAOZUVSX
      JUULJXKXMXKMXJUUNUVCWOUULXMBCXAXJUUNUULUVCUVBUVJWPWQVTXJUWALAXJXRAMZQZXTY
      FXRXONZYGXRXONZQZUVTXTXSJBOZXSJCOZQUWDUWGXSJBCVPUWDUWHUWEUWIUWFUWDXGXHUWC
      UWHUWEPXGXHXIUWCVDZXGXHXIUWCVHZXJUWCVFZJABDFXOXRGYJIWFSUWDXGXIUWCUWIUWFPU
      WJXGXHXIUWCVKZUWLJACDFXOXRGYJIWFSWJWKUWDYLYMYNUWCUWGUVTVCUWDXGYLUWJYOVEUW
      DXGXHYMUWJUWKYQRUWDXGXIYNUWJUWMYSRUWLAEFXOYFYGXRGYJHWGWHWIVTUUDYDUUMUWBQU
      UDXQUUMYCUWBUUDXPUULJXKXNYHXMXOVRVSUUDYBUWALAUUDYAUVTXTXNYHXRXOWRWSVSWTXB
      RTXCVJXDXEXF $.
      $( [5-Mar-2012] $)
  $}

