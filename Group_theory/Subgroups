$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Subgroups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SubGrp $.

  $( Extend class notation to include the class of subgroups. $)
  csubg $a class SubGrp $.

  ${
    $d g h s $.
    $( Define the set of subgroups of ` g ` . $)
    df-subg $a |- SubGrp = { <. g , s >. |
                             ( g e. Grp /\ s = { h e. Grp | h C_ g } ) } $.
  $}

  ${
    $d G g h s $.  $d H h $.
    $( The predicate "is a subgroup of ` G ` ."  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    issubg $p |- ( H e. ( SubGrp ` G ) <->
                   ( G e. Grp /\ H e. Grp /\ H C_ G ) ) $=
      ( vg vs vh csubg cfv wcel cgr wss w3a wa 3anass wn cdm c0 wceq cv crab
      cab cvv df-rab cpw df-pw visset pwex eqeltrri simpr ss2abi ssexi eqeltri
      df-subg dmopab2 eleq2i biimpi con3i ndmfv n0i con2i 3syl con4i abssexg
      ancom abbii eqtri syl5eqel sseq2 rabbidv fvopab4g mpdan eleq2d sseq1
      elrab syl6bb biimpd mpcom sylanbrc biimpar 3impb impbii ) BAFGZHZAIHZBIHZ
      BAJZKZWFWCWDWELZWBWCWDWEMWCWBWCNAFOZHZNWAPQZWBNWIWCWIWCWHIACDIERZCRZJZEIS
      ZFWNWKIHZWMLZETZUAWMEIUBWQWMETZWLUCWRUAEWLUDWLCUEUFUGWPWMEWOWMUHUIUJUKCED
      ULZUMUNUOUPAFUQWBWJWABURUSUTVAZWCWBWGWTWCWBWGWCWBBWKAJZEISZHWGWCWAXBBWCXB
      UAHWAXBQWCXAWOLZETZUAXBWOEAIVBXBWOXALZETXDXAEIUBXEXCEWOXAVCVDVEVFCDAWNXBI
      UAFWLAQWMXAEIWLAWKVGVHWSVIVJVKXAWEEBIWKBAVLVMVNZVOVPVQWCWDWEWBWCWBWGXFVRV
      SVT $.
      $( [3-Mar-2008] $)
  $}

  ${
    subgres.1 $e |- W = ran H $.
    $( A subgroup operation is the restriction of its parent group operation to
       its underlying set.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgres $p |- ( H e. ( SubGrp ` G ) -> H = ( G |` ( W X. W ) ) ) $=
      ( csubg cfv wcel cxp cres wfun wss cdm wceq cgr issubg simp1bi crn wfo
      eqid grpfo fofun syl simp3bi wf simp2bi fof 3syl fdm eqimss2 fun2ssres
      syl111anc wfn fofn fnresdm eqtr2d ) BAEFGZACCHZIZBUQIZBUPAJZBAKZUQBLZKZUR
      USMUPANGZUTUPVDBNGZVAABOZPVDAQZVGHZVGARUTAVGVGSTVHVGAUAUBUBUPVDVEVAVFUCUP
      UQCBUDZVBUQMVCUPVEUQCBRZVIUPVDVEVAVFUEZBCDTZUQCBUFUGUQCBUHUQVBUIUGUQABUJU
      KUPVEVJUSBMZVKVLVJBUQULVMUQCBUMUQBUNUBUGUO $.
      $( [3-Mar-2008] $)

    $( The result of a subgroup operation is the same as the result of its
       parent operation.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgopr $p |- ( H e. ( SubGrp ` G ) ->
                    ( ( A e. W /\ B e. W ) -> ( A H B ) = ( A G B ) ) ) $=
      ( csubg cfv wcel wa co wceq cxp cres subgres adantr opreqd oprvres
      adantl eqtrd ex ) DCGHIZAEIBEIJZABDKZABCKZLUBUCJZUDABCEEMNZKZUEUFDUGABUBD
      UGLUCCDEFOPQUCUHUELUBABEECRSTUA $.
      $( [3-Mar-2008] $)
  $}

  ${
    subgrnss.1 $e |- X = ran G $.
    subgrnss.2 $e |- W = ran H $.
    $( The underlying set of a subgroup is a subset of its parent group's
       underlying set.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgrnss $p |- ( H e. ( SubGrp ` G ) -> W C_ X ) $=
      ( csubg cfv wcel crn wss cxp cima imassrn cres subgres rneqd df-ima
      syl6eqr sseq1d mpbiri 3sstr4g ) BAGHIZBJZAJZCDUCUDUEKACCLZMZUEKAUFNUCUDUG
      UEUCUDAUFOZJUGUCBUHABCFPQAUFRSTUAFEUB $.
      $( [3-Mar-2008] $)
  $}

  ${
    subgid.1 $e |- U = ( Id ` G ) $.
    subgid.2 $e |- T = ( Id ` H ) $.
    $( The identity element of a subgroup is the same as its parent's.
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgid $p |- ( H e. ( SubGrp ` G ) -> T = U ) $=
      ( csubg cfv wcel wceq co crn cgr wss issubg simp2bi eqid grpidcl syl
      subgopr mp2and grplid syl11anc eqtr3d wb simp1bi subgrnss sseldd grpid
      mpbird ) DCGHIZABJZAACKZAJZUKAADKZUMAUKADLZIZUQUOUMJUKDMIZUQUKCMIZURDCNZC
      DOZPZADUPUPQZFRSZVDAACDUPVCTUAUKURUQUOAJVBVDAADUPVCFUBUCUDUKUSACLZIULUNUE
      UKUSURUTVAUFUKUPVEACDUPVEVEQZVCUGVDUHABCVEVFEUIUCUJ $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d G x y $.  $d H x y $.  $d Y x y $.
    issubgilem.1 $e |- ( ( x e. Y /\ y e. Y ) -> ( x H y ) = ( x G y ) ) $.
    $( Lemma for ~ issubgi . $)
    issubgilem $p |- ( ( A e. Y /\ B e. Y ) -> ( A H B ) = ( A G B ) ) $=
      ( cv co wceq opreq1 eqeq12d opreq2 vtocl2ga ) AIZBIZFJZPQEJZKCQFJZCQEJZKC
      DFJZCDEJZKABCDGGPCKRTSUAPCQFLPCQELMQDKTUBUAUCQDCFNQDCENMHO $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d G a b $.  $d H a b x y $.  $d H b x y z $.  $d N a b y $.
    $d U a b x y $.  $d U b x y z $.  $d Y a b x y $.  $d Y b x y z $.
    issubgi.1 $e |- G e. Grp $.
    issubgi.2 $e |- X = ran G $.
    issubgi.3 $e |- U = ( Id ` G ) $.
    issubgi.4 $e |- N = ( inv ` G ) $.
    issubgi.5 $e |- Y C_ X $.
    issubgi.6 $e |- H = ( G |` ( Y X. Y ) ) $.
    issubgi.7 $e |- ( ( x e. Y /\ y e. Y ) -> ( x G y ) e. Y ) $.
    issubgi.8 $e |- U e. Y $.
    issubgi.9 $e |- ( x e. Y -> ( N ` x ) e. Y ) $.
    $( Properties that determine a subgroup.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    issubgi $p |- H e. ( SubGrp ` G ) $=
      ( vz va vb cgr wcel wss csubg cfv cv crn cvv rnexg ax-mp eqeltri ssexi
      cxp wf wfn co wral ffnoprv cres wfo grpfo fof syl xpss12 mp2an fssres
      feq1i mpbir ffn wa oprvres opreqi syl5eq issubgilem eqeltrd rgen2a
      mpbir2an w3a wceq grpass mpan sseli syl3an sylan 3impa opreq1d 3adant3
      eqtrd foprcl sylan2 3impb opreq2d 3adant1 3eqtr4d grplid sylancr mpancom
      grplinv isgrpi resss eqsstri issubg biimpri mp3an ) DUAUBZEUAUBZEDUCZEDUD
      UEUBZIABRCEAUFZFUEZHHGGDUGZUHJXEXKUHUBIDUAUIUJUKMULHHUMZHEUNEXLUOZXIBUFZE
      UPZHUBZBHUQAHUQABHHHEURXLGEUNZXMXQXLGDXLUSZUNZGGUMZGDUNZXLXTUCZXSXEYAIXEX
      TGDUTYADGJVAXTGDVBVCUJHGUCZYCYBMMHGHGVDVEXTGXLDVFVEXLGEXRNVGVHXLGEVIUJXPA
      BHXIHUBZXNHUBZVJZXOXIXNDUPZHSTXIXNDEHSUFZHUBTUFZHUBVJYHYIXRUPYHYIDUPYHYIE
      UPYHYIHHDVKEXRYHYINVLVMZVNZOVOZVPVQZYDYERUFZHUBZVRZYGYNDUPZXIXNYNDUPZDUPZ
      XOYNEUPZXIXNYNEUPZEUPZXIGUBZXNGUBZYNGUBZYQYSVSZYDYEYOXEUUCUUDUUEVRUUFIXIX
      NYNDGJVTWAHGXIMWBZHGXNMWBHGYNMWBWCYPYTXOYNDUPZYQYDYEYOYTUUHVSZXPYOUUIYFST
      XOYNDEHYJVNYLWDWEYDYEUUHYQVSYOYFXOYGYNDYKWFWGWHYPUUBXIUUADUPZYSYDYEYOUUBU
      UJVSZYDUUAHUBUUKYEYOVJZSTXIUUADEHYJVNXNYNHHHEYMWIWJWKYEYOUUJYSVSYDUULUUAY
      RXIDSTXNYNDEHYJVNWLWMWHWNPYDCXIEUPZCXIDUPZXICHUBYDUUMUUNVSPSTCXIDEHYJVNWA
      XEUUCUUNXIVSYDXICDGJKWOIUUGWPWHQYDXJXIEUPZXJXIDUPZCXJHUBYDUUOUUPVSQSTXJXI
      DEHYJVNWQXEUUCUUPCVSYDXICDFGJKLWRIUUGWPWHWSEXRDNDXLWTXAXHXEXFXGVRDEXBXCXD
      $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d G x y $.  $d H x y $.
    $( A subgroup of an Abelian group is Abelian.  (Contributed by Paul
       Chapman, 25-Apr-2008.) $)
    subgabl $p |- ( ( G e. Abel /\ H e. ( SubGrp ` G ) ) -> H e. Abel ) $=
      ( vx vy cabl wcel csubg cfv wa cv co wceq crn wral simpr eqid subgrnss
      sseld anim12d wi cgr isabl simprbi ra42 syl sylan9r imp subgopr adantl
      ancomsd 3eqtr4d ex r19.21aivv biimpri wss issubg simp2bi sylan syl11anc
      ) AEFZBAGHFZIZVACJZDJZBKZVDVCBKZLZDBMZNCVHNZBEFZUTVAOVBVGCDVHVHVBVCVHFZVD
      VHFZIZVGVBVMIVCVDAKZVDVCAKZVEVFVBVMVNVOLZVAVMVCAMZFZVDVQFZIZUTVPVAVKVRVLV
      SVAVHVQVCABVHVQVQPZVHPZQZRVAVHVQVDWCRSUTVPDVQNCVQNZVTVPTUTAUAFZWDCDAVQWAU
      BUCVPCDVQVQUDUEUFUGVBVMVEVNLZVAVMWFTUTVCVDABVHWBUHUIUGVBVMVFVOLZVAVMWGTUT
      VAVLVKWGVDVCABVHWBUHUJUIUGUKULUMBUAFZVIVJVAVJWHVIICDBVHWBUBUNVAWEWHBAUOAB
      UPUQURUS $.
      $( [25-Apr-2008] $)
  $}


