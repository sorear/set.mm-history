$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Subgroups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SubGrpOp $.

  $( Extend class notation to include the class of subgroups. $)
  csubg $a class SubGrpOp $.

  ${
    $d g h s $.
    $( Define the set of subgroups of ` g ` . $)
    df-subg $a |- SubGrpOp = { <. g , s >. |
                             ( g e. GrpOp /\ s = { h e. GrpOp | h C_ g } ) } $.
  $}

  ${
    $d G g h s $.  $d H h $.
    $( The predicate "is a subgroup of ` G ` ."  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    issubg $p |- ( H e. ( SubGrpOp ` G ) <->
                   ( G e. GrpOp /\ H e. GrpOp /\ H C_ G ) ) $=
      ( vg vs vh csubg cfv wcel cgr wss w3a wa wn cdm wceq crab cab cvv df-rab
      cv cpw df-pw vex pwex eqeltrri simpr ss2abi ssexi eqeltri df-subg dmopab2
      eleq2i biimpi con3i ndmfv n0i con2i 3syl con4i ancom abbii eqtri syl5eqel
      c0 abssexg sseq2 rabbidv fvopab4g mpdan eleq2d sseq1 syl6bb biimpd 3anass
      elrab mpcom sylanbrc biimpar 3impb impbii ) BAFGZHZAIHZBIHZBAJZKZWBWCWDWE
      LZWFWCWBWCMAFNZHZMWAVDOZWBMWIWCWIWCWHIACDIETZCTZJZEIPZFWNWKIHZWMLZEQZRWME
      ISWQWMEQZWLUAWRREWLUBWLCUCUDUEWPWMEWOWMUFUGUHUICEDUJZUKULUMUNAFUOWBWJWABU
      PUQURUSZWCWBWGWTWCWBWGWCWBBWKAJZEIPZHWGWCWAXBBWCXBRHWAXBOWCXBXAWOLZEQZRXB
      WOXALZEQXDXAEISXEXCEWOXAUTVAVBWOEAIVEVCCDAWNXBIRFWLAOWMXAEIWLAWKVFVGWSVHV
      IVJXAWEEBIWKBAVKVOVLZVMVPWCWDWEVNVQWCWDWEWBWCWBWGXFVRVSVT $.
      $( [3-Mar-2008] $)
  $}

  ${
    subgres.1 $e |- W = ran H $.
    $( A subgroup operation is the restriction of its parent group operation to
       its underlying set.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgres $p |- ( H e. ( SubGrpOp ` G ) -> H = ( G |` ( W X. W ) ) ) $=
      ( csubg cfv wcel cxp cres wfun wss cdm wceq cgr issubg simp1bi wfo grpofo
      syl 3syl crn eqid fofun simp3bi simp2bi fof fdm eqimss2 fun2ssres syl3anc
      wf wfn fofn fnresdm eqtr2d ) BAEFGZACCHZIZBUQIZBUPAJZBAKZUQBLZKZURUSMUPAN
      GZUTUPVDBNGZVAABOZPVDAUAZVGHZVGAQUTAVGVGUBRVHVGAUCSSUPVDVEVAVFUDUPUQCBUKZ
      VBUQMVCUPVEUQCBQZVIUPVDVEVAVFUEZBCDRZUQCBUFTUQCBUGUQVBUHTUQABUIUJUPVEVJUS
      BMZVKVLVJBUQULVMUQCBUMUQBUNSTUO $.
      $( [3-Mar-2008] $)

    $( The result of a subgroup operation is the same as the result of its
       parent operation.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgopr $p |- ( H e. ( SubGrpOp ` G ) ->
                    ( ( A e. W /\ B e. W ) -> ( A H B ) = ( A G B ) ) ) $=
      ( csubg cfv wcel wa co wceq cxp cres subgres adantr opreqd oprvres adantl
      eqtrd ex ) DCGHIZAEIBEIJZABDKZABCKZLUBUCJZUDABCEEMNZKZUEUFDUGABUBDUGLUCCD
      EFOPQUCUHUELUBABEECRSTUA $.
      $( [3-Mar-2008] $)
  $}

  ${
    subgrnss.1 $e |- X = ran G $.
    subgrnss.2 $e |- W = ran H $.
    $( The underlying set of a subgroup is a subset of its parent group's
       underlying set.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgrnss $p |- ( H e. ( SubGrpOp ` G ) -> W C_ X ) $=
      ( csubg cfv wcel crn wss cima imassrn subgres rneqd df-ima syl6eqr sseq1d
      cxp cres mpbiri 3sstr4g ) BAGHIZBJZAJZCDUCUDUEKACCSZLZUEKAUFMUCUDUGUEUCUD
      AUFTZJUGUCBUHABCFNOAUFPQRUAFEUB $.
      $( [3-Mar-2008] $)
  $}

  ${
    subgid.1 $e |- U = ( Id ` G ) $.
    subgid.2 $e |- T = ( Id ` H ) $.
    $( The identity element of a subgroup is the same as its parent's.
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgid $p |- ( H e. ( SubGrpOp ` G ) -> T = U ) $=
      ( csubg cfv wcel wceq co crn cgr wss issubg simp2bi eqid grpoidcl syl2anc
      syl subgopr mp2and grpolid eqtr3d simp1bi subgrnss sseldd grpoid mpbird
      wb ) DCGHIZABJZAACKZAJZUKAADKZUMAUKADLZIZUQUOUMJUKDMIZUQUKCMIZURDCNZCDOZP
      ZADUPUPQZFRTZVDAACDUPVCUAUBUKURUQUOAJVBVDAADUPVCFUCSUDUKUSACLZIULUNUJUKUS
      URUTVAUEUKUPVEACDUPVEVEQZVCUFVDUGABCVEVFEUHSUI $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d G x y $.  $d H x y $.  $d Y x y $.
    issubgilem.1 $e |- ( ( x e. Y /\ y e. Y ) -> ( x H y ) = ( x G y ) ) $.
    $( Lemma for ~ issubgi . $)
    issubgilem $p |- ( ( A e. Y /\ B e. Y ) -> ( A H B ) = ( A G B ) ) $=
      ( cv co wceq opreq1 eqeq12d opreq2 vtocl2ga ) AIZBIZFJZPQEJZKCQFJZCQEJZKC
      DFJZCDEJZKABCDGGPCKRTSUAPCQFLPCQELMQDKTUBUAUCQDCFNQDCENMHO $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d G a b $.  $d H a b x y $.  $d H b x y z $.  $d N a b y $.
    $d U a b x y $.  $d U b x y z $.  $d Y a b x y $.  $d Y b x y z $.
    issubgi.1 $e |- G e. GrpOp $.
    issubgi.2 $e |- X = ran G $.
    issubgi.3 $e |- U = ( Id ` G ) $.
    issubgi.4 $e |- N = ( inv ` G ) $.
    issubgi.5 $e |- Y C_ X $.
    issubgi.6 $e |- H = ( G |` ( Y X. Y ) ) $.
    issubgi.7 $e |- ( ( x e. Y /\ y e. Y ) -> ( x G y ) e. Y ) $.
    issubgi.8 $e |- U e. Y $.
    issubgi.9 $e |- ( x e. Y -> ( N ` x ) e. Y ) $.
    $( Properties that determine a subgroup.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    issubgi $p |- H e. ( SubGrpOp ` G ) $=
      ( wcel co wceq vz va vb cgr wss csubg cfv crn cvv rnexg ax-mp eqeltri cxp
      cv ssexi wf wfn wral ffnoprv wfo grpofo fof syl xpss12 mp2an fssres feq1i
      mpbir ffn wa opreqi oprvres syl5eq issubgilem eqeltrd rgen2a mpbir2an w3a
      cres sseli grpoass syl3an sylan 3impa opreq1d 3adant3 eqtrd foprcl sylan2
      mpan 3impb opreq2d 3adant1 3eqtr4d grpolid sylancr mpancom grpolinv resss
      isgrpoi eqsstri issubg biimpri mp3an ) DUDRZEUDRZEDUEZEDUFUGRZIABUACEAUNZ
      FUGZHHGGDUHZUIJXEXKUIRIDUDUJUKULMUOHHUMZHEUPEXLUQZXIBUNZESZHRZBHURAHURABH
      HHEUSXLGEUPZXMXQXLGDXLVSZUPZGGUMZGDUPZXLXTUEZXSXEYAIXEXTGDUTYADGJVAXTGDVB
      VCUKHGUEZYCYBMMHGHGVDVEXTGXLDVFVEXLGEXRNVGVHXLGEVIUKXPABHXIHRZXNHRZVJZXOX
      IXNDSZHUBUCXIXNDEHUBUNZHRUCUNZHRVJYHYIESYHYIXRSYHYIDSEXRYHYINVKYHYIHHDVLV
      MZVNZOVOZVPVQZYDYEUAUNZHRZVRZYGYNDSZXIXNYNDSZDSZXOYNESZXIXNYNESZESZYDXIGR
      ZYEXNGRZYOYNGRZYQYSTZHGXIMVTZHGXNMVTHGYNMVTXEUUCUUDUUEVRUUFIXIXNYNDGJWAWJ
      WBYPYTXOYNDSZYQYDYEYOYTUUHTZYFXPYOUUIYLUBUCXOYNDEHYJVNWCWDYDYEUUHYQTYOYFX
      OYGYNDYKWEWFWGYPUUBXIUUADSZYSYDYEYOUUBUUJTZYEYOVJZYDUUAHRUUKXNYNHHHEYMWHU
      BUCXIUUADEHYJVNWIWKYEYOUUJYSTYDUULUUAYRXIDUBUCXNYNDEHYJVNWLWMWGWNPYDCXIES
      ZCXIDSZXICHRYDUUMUUNTPUBUCCXIDEHYJVNWJYDXEUUCUUNXITIUUGXICDGJKWOWPWGQYDXJ
      XIESZXJXIDSZCXJHRYDUUOUUPTQUBUCXJXIDEHYJVNWQYDXEUUCUUPCTIUUGXICDFGJKLWRWP
      WGWTEXRDNDXLWSXAXHXEXFXGVRDEXBXCXD $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d G x y $.  $d H x y $.
    $( A subgroup of an Abelian group is Abelian.  (Contributed by Paul
       Chapman, 25-Apr-2008.) $)
    subgabl $p |- ( ( G e. AbelOp /\ H e. ( SubGrpOp ` G ) )
           -> H e. AbelOp ) $=
      ( vx vy cabl wcel wa cv co wceq crn wral eqid sseld wi cgr isablo subgopr
      imp adantl csubg cfv subgrnss anim12d simprbi syl sylan9r ancomsd 3eqtr4d
      simpr ra42 ex ralrimivv wss issubg simp2bi biimpri sylan syl2anc ) AEFZBA
      UAUBFZGZVACHZDHZBIZVDVCBIZJZDBKZLCVHLZBEFZUTVAUJVBVGCDVHVHVBVCVHFZVDVHFZG
      ZVGVBVMGVCVDAIZVDVCAIZVEVFVBVMVNVOJZVAVMVCAKZFZVDVQFZGZUTVPVAVKVRVLVSVAVH
      VQVCABVHVQVQMZVHMZUCZNVAVHVQVDWCNUDUTVPDVQLCVQLZVTVPOUTAPFZWDCDAVQWAQUEVP
      CDVQVQUKUFUGSVBVMVEVNJZVAVMWFOUTVCVDABVHWBRTSVBVMVFVOJZVAVMWGOUTVAVLVKWGV
      DVCABVHWBRUHTSUIULUMVABPFZVIVJVAWEWHBAUNABUOUPVJWHVIGCDBVHWBQUQURUS $.
      $( [25-Apr-2008] $)
  $}


