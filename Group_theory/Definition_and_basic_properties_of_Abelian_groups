$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Definition and basic properties of Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c AbelOp $.

  $( Extend class notation with the class of all Abelian group operations. $)
  cabl $a class AbelOp $.

  ${
    $d g x y $.
    $( Define the class of all Abelian group operations. $)
    df-ablo $a |- AbelOp
     = { g e. GrpOp | A. x e. ran g A. y e. ran g ( x g y ) = ( y g x ) } $.
  $}

  ${
    $d g x y G $.  $d g x y X $.
    isabl.1 $e |- X = ran G $.
    $( The predicate "is an Abelian (commutative) group operation." $)
    isablo $p |- ( G e. AbelOp <-> ( G e. GrpOp /\
             A. x e. X A. y e. X ( x G y ) = ( y G x ) ) ) $=
      ( vg cv co wceq crn wral cgr cabl rneq syl6eqr raleq raleqbi1dv syl oveq
      wb eqeq12d 2ralbidv bitrd df-ablo elrab2 ) AGZBGZFGZHZUGUFUHHZIZBUHJZKZAU
      LKZUFUGCHZUGUFCHZIZBDKADKZFCLMUHCIZUNUKBDKZADKZURUSULDIUNVATUSULCJDUHCNEO
      UMUTAULDUKBULDPQRUSUKUQABDDUSUIUOUJUPUFUGUHCSUGUFUHCSUAUBUCABFUDUE $.
      $( [2-Nov-2006] $)
  $}

  ${
    $d x y G $.
    $( An Abelian group operation is a group operation. $)
    ablogrpo $p |- ( G e. AbelOp -> G e. GrpOp ) $=
      ( vx vy cabl wcel cgr cv co wceq crn wral eqid isablo simplbi ) ADEAFEBGZ
      CGZAHPOAHICAJZKBQKBCAQQLMN $.
      $( [2-Nov-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y G $.  $d x y X $.
    ablcom.1 $e |- X = ran G $.
    $( An Abelian group operation is commutative. $)
    ablocom $p |- ( ( G e. AbelOp /\ A e. X /\ B e. X ) ->
                 ( A G B ) = ( B G A ) ) $=
      ( vx vy cabl wcel co wceq cv wral cgr isablo simprbi oveq1 oveq2 eqeq12d
      wa rcla42v syl5com 3impib ) CHIZADIZBDIZABCJZBACJZKZUDFLZGLZCJZUKUJCJZKZG
      DMFDMZUEUFTUIUDCNIUOFGCDEOPUNUIAUKCJZUKACJZKFGABDDUJAKULUPUMUQUJAUKCQUJAU
      KCRSUKBKUPUGUQUHUKBACRUKBACQSUAUBUC $.
      $( [2-Nov-2006] $)

    $( Commutative/associative law for Abelian groups. $)
    ablo32 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cabl wcel w3a wa co ablocom 3adant3r1 oveq2d cgr ablogrpo grpoass sylan
      wceq 3ancomb sylan2b 3eqtr4d ) DGHZAEHZBEHZCEHZIZJZABCDKZDKZACBDKZDKZABDK
      CDKZACDKBDKZUHUIUKADUCUEUFUIUKSUDBCDEFLMNUCDOHZUGUMUJSDPZABCDEFQRUCUOUGUN
      ULSZUPUGUOUDUFUEIUQUDUEUFTACBDEFQUARUB $.
      $( [26-Apr-2007] $)

    $( Commutative/associative law for Abelian groups. $)
    ablo4 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X ) /\ ( C e. X /\
   D e. X ) ) -> ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( wcel wa wceq w3a simprlr simprrl 3jca syldan grpocl 3expb grpoass sylan
      co cabl simprll ablo32 ablogrpo adantrr simprrr adantrlr adantrrr 3eqtr3d
      oveq1d cgr 3impb ) EUAHZAFHZBFHZIZCFHZDFHZIZABETZCDETETZACETZBDETETZJUMUP
      USIZIZUTCETZDETZVBBETZDETZVAVCVEVFVHDEUMVDUNUOUQKVFVHJVEUNUOUQUMUNUOUSUBU
      MUNUOUSLUMUPUQURMNABCEFGUCOUJUMEUKHZVDVGVAJZEUDZVJVDUTFHZUQURKVKVJVDIZVMU
      QURVJUPVMUSVJUNUOVMABEFGPQUEVJUPUQURMVJUPUQURUFZNUTCDEFGROSUMVJVDVIVCJZVL
      VJVDVBFHZUOURKVPVNVQUOURVJUPUQVQURVJUNUQVQUOVJUNUQVQACEFGPQUGUHVJUNUOUSLV
      ONVBBDEFGROSUIUL $.
      $( [26-Apr-2007] $)
  $}

  ${
    $d x y G $.  $d x y X $.
    isabli.1 $e |- G e. GrpOp $.
    isabli.2 $e |- dom G = ( X X. X ) $.
    isabli.3 $e |- ( ( x e. X /\ y e. X ) -> ( x G y ) = ( y G x ) ) $.
    $( Properties that determine an Abelian group operation. $)
    isabloi $p |- G e. AbelOp $=
      ( cabl wcel cgr cv co wceq wral grporn isablo rgen2a mpbir2an ) CHICJIAKZ
      BKZCLTSCLMZBDNADNABCDCDEFOPEUAABDGQR $.
      $( [9-Apr-2008] $) $( [5-Nov-2006] $)
  $}

  ${
    abldiv.1 $e |- X = ran G $.
    abldiv.3 $e |- D = ( /g ` G ) $.
    $( Law for group multiplication and division. $)
    ablmuldiv $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
     ( ( A G B ) D C ) = ( ( A D C ) G B ) ) $=
      ( cabl wcel w3a wa co wceq ablocom 3adant3r3 oveq1d 3ancoma cgr ablogrpo
      grpomuldivass sylan sylan2b simpr2 grpodivcl syl3an1 3adant3r2 jca syldan
      3expb 3eqtrd ) EIJZAFJZBFJZCFJZKZLZABEMZCDMBAEMZCDMZBACDMZEMZVABEMZUQURUS
      CDULUMUNURUSNUOABEFGOPQUPULUNUMUOKZUTVBNZUMUNUORULESJZVDVEETZBACDEFGHUAUB
      UCULUPUNVAFJZLVBVCNZUQUNVHULUMUNUOUDULUMUOVHUNULVFUMUOVHVGACDEFGHUEUFUGUH
      ULUNVHVIBVAEFGOUJUIUK $.
      $( [15-Feb-2008] $)

    $( Law for double group division. $)
    abldivdiv $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
    ( A D ( B D C ) ) = ( ( A D B ) G C ) ) $=
      ( cabl wcel w3a wa co cgr ablogrpo grpodivdiv sylan 3ancomb grpomuldivass
      wceq ablmuldiv eqtr3d sylan2b eqtrd ) EIJZAFJZBFJZCFJZKZLABCDMDMZACBDMEMZ
      ABDMCEMZUEENJZUIUJUKTEOZABCDEFGHPQUIUEUFUHUGKZUKULTUFUGUHRUEUOLACEMBDMZUK
      ULUEUMUOUPUKTUNACBDEFGHSQACBDEFGHUAUBUCUD $.
      $( [29-Feb-2008] $)

    $( Law for double group division. $)
    abldivdiv4 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
    ( ( A D B ) D C ) = ( A D ( B G C ) ) ) $=
      ( cabl wcel w3a wa co cgn cfv cgr wceq ablogrpo simpl grpodivcl 3adant3r3
      simpr3 eqid grpodivval syl3anc sylan simpr1 simpr2 simp3 grpoinvcl syl2an
      3jca abldivdiv syldan grpodivinv syl3an1 3adant3r1 oveq2d 3eqtr2d ) EIJZA
      FJZBFJZCFJZKZLZABDMZCDMZVFCENOZOZEMZABVIDMZDMZABCEMZDMUTEPJZVDVGVJQZERZVN
      VDLVNVFFJZVCVOVNVDSVNVAVBVQVCABDEFGHTUAVNVAVBVCUBVFCDEVHFGVHUCZHUDUEUFUTV
      DVAVBVIFJZKVLVJQVEVAVBVSUTVAVBVCUGUTVAVBVCUHUTVNVCVSVDVPVAVBVCUICEVHFGVRU
      JUKULABVIDEFGHUMUNVEVKVMADUTVBVCVKVMQZVAUTVNVBVCVTVPBCDEVHFGVRHUOUPUQURUS
      $.
      $( [29-Feb-2008] $)

    $( Swap the second and third terms in a double division. $)
    ablodiv32 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
   ( ( A D B ) D C ) = ( ( A D C ) D B ) ) $=
      ( cabl wcel w3a wa co ablocom 3adant3r1 oveq2d abldivdiv4 3ancomb sylan2b
      wceq 3eqtr4d ) EIJZAFJZBFJZCFJZKZLZABCEMZDMACBEMZDMZABDMCDMACDMBDMZUGUHUI
      ADUBUDUEUHUITUCBCEFGNOPABCDEFGHQUFUBUCUEUDKUKUJTUCUDUERACBDEFGHQSUA $.
      $( [29-Feb-2008] $)

    $( Group theory analog of ~ nnncan . $)
    ablnnncan $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
      ( ( A D ( B D C ) ) D C ) = ( A D B ) ) $=
      ( cabl wcel w3a wa wceq simpr1 cgr ablogrpo grpodivcl syl3an1 3adant3r1
      co simpr3 3jca abldivdiv4 syldan grponpcan oveq2d eqtrd ) EIJZAFJZBFJZCFJ
      ZKZLZABCDTZDTCDTZAUNCETZDTZABDTUHULUIUNFJZUKKUOUQMUMUIURUKUHUIUJUKNUHUJUK
      URUIUHEOJZUJUKUREPZBCDEFGHQRSUHUIUJUKUAUBAUNCDEFGHUCUDUMUPBADUHUJUKUPBMZU
      IUHUSUJUKVAUTBCDEFGHUERSUFUG $.
      $( [29-Feb-2008] $)

    $( Group theory analog of ~ nncan . $)
    ablnncan $p |- ( ( G e. AbelOp /\ A e. X /\ B e. X ) ->
             ( A D ( A D B ) ) = B ) $=
      ( cabl wcel w3a co cgi cfv wceq wa id 3anidm12 abldivdiv sylan2 sylan cgr
      3impb ablogrpo eqid grpodivid 3adant3 oveq1d grpolid 3adant2 3eqtrd ) DHI
      ZAEIZBEIZJZAABCKCKZAACKZBDKZDLMZBDKZBUKULUMUOUQNZULUMOUKULULUMJZUTULUMVAV
      APQAABCDEFGRSUBUNUPURBDUKULUPURNZUMUKDUAIZULVBDUCZACURDEFGURUDZUETUFUGUKU
      MUSBNZULUKVCUMVFVDBURDEFVEUHTUIUJ $.
      $( [7-Mar-2008] $)

    $( Group theory analog of ~ nnncan1 . $)
    ablnnncan1 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
         ( ( A D B ) D ( A D C ) ) = ( C D B ) ) $=
      ( cabl wcel w3a wa co wceq simpr1 simpr2 cgr ablogrpo grpodivcl 3adant3r2
      syl3an1 3jca ablodiv32 syldan ablnncan oveq1d eqtrd ) EIJZAFJZBFJZCFJZKZL
      ZABDMACDMZDMZAUNDMZBDMZCBDMUHULUIUJUNFJZKUOUQNUMUIUJURUHUIUJUKOUHUIUJUKPU
      HUIUKURUJUHEQJUIUKURERACDEFGHSUATUBABUNDEFGHUCUDUMUPCBDUHUIUKUPCNUJACDEFG
      HUETUFUG $.
      $( [7-Mar-2008] $)
  $}

  ${
    $d A k m $.  $d B k m $.  $d G k m $.  $d K m $.  $d P k m $.  $d X k m $.
    gxdi.1 $e |- X = ran G $.
    gxdi.2 $e |- P = ( ^g ` G ) $.
    $( Distribution of group power over group operation for abelian groups.
       (Contributed by Paul Chapman, 17-Apr-2009.) $)
    gxdi $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X ) /\ K e. ZZ ) ->
                 ( ( A G B ) P K ) = ( ( A P K ) G ( B P K ) ) ) $=
      ( wcel co wceq wi cc0 oveq2 oveq12d eqeq12d cfv 3ad2ant1 syl2anc syl3anc
      vm vk cabl wa cz cv cneg caddc w3a cgi cgr ablogrpo eqid grpoidcl grpolid
      syl simp2 gx0 simp3 grpocl syl3an1 3eqtr4rd cn0 nn0z gxsuc oveq1 3ad2ant3
      c1 simp1 gxcl ablo4 syl122anc 3eqtrd eqtr4d 3expia ex syl5 cn nnz ablocom
      cgn gxneg wb eqeq1 mpbird fveq2d grpoinvop zindd 3expb 3impia ) DUCIZAFIZ
      BFIZUDEUEIZABDJZECJZAECJZBECJZDJZKZWKWLWMWNWTLWOUAUFZCJZAXACJZBXACJZDJZKW
      OMCJZAMCJZBMCJZDJZKWOUBUFZCJZAXJCJZBXJCJZDJZKZWOXJUGZCJZAXPCJZBXPCJZDJZKZ
      WOXJVHUHJZCJZAYBCJZBYBCJZDJZKZWTWKWLWMUIZUAUBEXAMKZXBXFXEXIXAMWOCNYIXCXGX
      DXHDXAMACNXAMBCNOPXAXJKZXBXKXEXNXAXJWOCNYJXCXLXDXMDXAXJACNXAXJBCNOPXAYBKZ
      XBYCXEYFXAYBWOCNYKXCYDXDYEDXAYBACNXAYBBCNOPXAXPKZXBXQXEXTXAXPWOCNYLXCXRXD
      XSDXAXPACNXAXPBCNOPXAEKZXBWPXEWSXAEWOCNYMXCWQXDWRDXAEACNXAEBCNOPYHDUJQZYN
      DJZYNXIXFYHDUKIZYNFIZYOYNKWKWLYPWMDULZRZYHYPYQYSYNDFGYNUMZUNUPYNYNDFGYTUO
      SYHXGYNXHYNDYHYPWLXGYNKYSWKWLWMUQZACYNDFGYTHURSYHYPWMXHYNKYSWKWLWMUSZBCYN
      DFGYTHURSOYHYPWOFIZXFYNKYSWKYPWLWMUUCYRABDFGUTVAZWOCYNDFGYTHURSVBXJVCIXJU
      EIZYHXOYGLZXJVDYHUUEUUFYHUUEXOYGYHUUEXOUIZYCXLADJZXMBDJZDJZYFUUGYCXKWODJZ
      XNWODJZUUJUUGYPUUCUUEYCUUKKYHUUEYPXOYSRZYHUUEUUCXOUUDRZYHUUEXOUQZWOCDXJFG
      HVETXOYHUUKUULKUUEXKXNWODVFVGUUGWKXLFIZXMFIZWLWMUULUUJKYHUUEWKXOWKWLWMVIR
      ZUUGYPWLUUEUUPUUMYHUUEWLXOUUARZUUOACDXJFGHVJTZUUGYPWMUUEUUQUUMYHUUEWMXOUU
      BRZUUOBCDXJFGHVJTZUUSUVAXLXMABDFGVKVLVMUUGYDUUHYEUUIDUUGYPWLUUEYDUUHKUUMU
      USUUOACDXJFGHVETUUGYPWMUUEYEUUIKUUMUVAUUOBCDXJFGHVETOVNVOVPVQXJVRIUUEYHXO
      YALZXJVSYHUUEUVCYHUUEXOYAUUGXQXLDWAQZQZXMUVDQZDJZXTUUGXQXKUVDQZXMXLDJZUVD
      QZUVGUUGYPUUCUUEXQUVHKUUMUUNUUOWOCDXJUVDFGUVDUMZHWBTUUGXKUVIUVDUUGXKUVIKZ
      XNUVIKZUUGWKUUPUUQUVMUURUUTUVBXLXMDFGVTTXOYHUVLUVMWCUUEXKXNUVIWDVGWEWFUUG
      YPUUQUUPUVJUVGKUUMUVBUUTXMXLDUVDFGUVKWGTVMUUGXRUVEXSUVFDUUGYPWLUUEXRUVEKU
      UMUUSUUOACDXJUVDFGUVKHWBTUUGYPWMUUEXSUVFKUUMUVAUUOBCDXJUVDFGUVKHWBTOVNVOV
      PVQWHWIWJ $.
      $( [17-Apr-2009] $)
  $}


