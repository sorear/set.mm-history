$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Definition and basic properties of Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c AbelOp $.

  $( Extend class notation with the class of all Abelian group operations. $)
  cabl $a class AbelOp $.

  ${
    $d g x y $.
    $( Define the class of all Abelian group operations. $)
    df-ablo $a |- AbelOp
     = { g e. GrpOp | A. x e. ran g A. y e. ran g ( x g y ) = ( y g x ) } $.
  $}

  ${
    $d g x y G $.  $d g x y X $.
    isabl.1 $e |- X = ran G $.
    $( The predicate "is an Abelian (commutative) group operation." $)
    isablo $p |- ( G e. AbelOp <-> ( G e. GrpOp /\
             A. x e. X A. y e. X ( x G y ) = ( y G x ) ) ) $=
      ( vg cv co wceq crn wral cgr cabl wb rneq syl6eqr raleq raleqbi1dv syl
      opreq eqeq12d 2ralbidv bitrd df-ablo elrab2 ) AGZBGZFGZHZUGUFUHHZIZBUHJZK
      ZAULKZUFUGCHZUGUFCHZIZBDKADKZFCLMUHCIZUNUKBDKZADKZURUSULDIUNVANUSULCJDUHC
      OEPUMUTAULDUKBULDQRSUSUKUQABDDUSUIUOUJUPUFUGUHCTUGUFUHCTUAUBUCABFUDUE $.
      $( [2-Nov-2006] $)
  $}

  ${
    $d x y G $.
    $( An Abelian group operation is a group operation. $)
    ablogrpo $p |- ( G e. AbelOp -> G e. GrpOp ) $=
      ( vx vy cabl wcel cgr cv co wceq crn wral eqid isablo simplbi ) ADEAFEBGZ
      CGZAHPOAHICAJZKBQKBCAQQLMN $.
      $( [2-Nov-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y G $.  $d x y X $.
    ablcom.1 $e |- X = ran G $.
    $( An Abelian group operation is commutative. $)
    ablocom $p |- ( ( G e. AbelOp /\ A e. X /\ B e. X ) ->
                 ( A G B ) = ( B G A ) ) $=
      ( vx vy cabl wcel co wceq cv wral wa cgr isablo simprbi opreq1 opreq2
      eqeq12d rcla42v syl5com 3impib ) CHIZADIZBDIZABCJZBACJZKZUDFLZGLZCJZUKUJC
      JZKZGDMFDMZUEUFNUIUDCOIUOFGCDEPQUNUIAUKCJZUKACJZKFGABDDUJAKULUPUMUQUJAUKC
      RUJAUKCSTUKBKUPUGUQUHUKBACSUKBACRTUAUBUC $.
      $( [2-Nov-2006] $)

    $( Commutative/associative law for Abelian groups. $)
    abl23 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cabl wcel w3a wa co wceq ablocom 3adant3r1 opreq2d cgr ablogrpo
      grpoass sylan 3ancomb sylan2b 3eqtr4d ) DGHZAEHZBEHZCEHZIZJZABCDKZDKZACBD
      KZDKZABDKCDKZACDKBDKZUHUIUKADUCUEUFUIUKLUDBCDEFMNOUCDPHZUGUMUJLDQZABCDEFR
      SUCUOUGUNULLZUPUGUOUDUFUEIUQUDUEUFTACBDEFRUASUB $.
      $( [26-Apr-2007] $)

    $( Commutative/associative law for Abelian groups. $)
    abl4 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X ) /\ ( C e. X /\
   D e. X ) ) -> ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( cabl wcel wa co wceq w3a simprll simprlr simprrl 3jca abl23 syldan
      opreq1d cgr ablogrpo grpocl 3expb adantrr simprrr grpoass sylan adantrlr
      adantrrr 3eqtr3d 3impb ) EHIZAFIZBFIZJZCFIZDFIZJZABEKZCDEKEKZACEKZBDEKEKZ
      LUMUPUSJZJZUTCEKZDEKZVBBEKZDEKZVAVCVEVFVHDEUMVDUNUOUQMVFVHLVEUNUOUQUMUNUO
      USNUMUNUOUSOUMUPUQURPQABCEFGRSTUMEUAIZVDVGVALZEUBZVJVDUTFIZUQURMVKVJVDJZV
      MUQURVJUPVMUSVJUNUOVMABEFGUCUDUEVJUPUQURPVJUPUQURUFZQUTCDEFGUGSUHUMVJVDVI
      VCLZVLVJVDVBFIZUOURMVPVNVQUOURVJUPUQVQURVJUNUQVQUOVJUNUQVQACEFGUCUDUIUJVJ
      UNUOUSOVOQVBBDEFGUGSUHUKUL $.
      $( [26-Apr-2007] $)
  $}

  ${
    $d x y G $.  $d x y X $.
    isabli.1 $e |- G e. GrpOp $.
    isabli.2 $e |- dom G = ( X X. X ) $.
    isabli.3 $e |- ( ( x e. X /\ y e. X ) -> ( x G y ) = ( y G x ) ) $.
    $( Properties that determine an Abelian group operation. $)
    isabloi $p |- G e. AbelOp $=
      ( cabl wcel cgr cv co wceq wral grprn isablo rgen2a mpbir2an ) CHICJIAKZB
      KZCLTSCLMZBDNADNABCDCDEFOPEUAABDGQR $.
      $( [9-Apr-2008] $) $( [5-Nov-2006] $)
  $}

  ${
    abldiv.1 $e |- X = ran G $.
    abldiv.3 $e |- D = ( /g ` G ) $.
    $( Law for group multiplication and division. $)
    ablmuldiv $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
     ( ( A G B ) D C ) = ( ( A D C ) G B ) ) $=
      ( cabl wcel w3a wa co wceq ablocom 3adant3r3 opreq1d 3ancoma cgr
      ablogrpo grpmuldivass sylan sylan2b simpr2 grpdivcl syl3an1 3adant3r2
      jca 3expb syldan 3eqtrd ) EIJZAFJZBFJZCFJZKZLZABEMZCDMBAEMZCDMZBACDMZEMZV
      ABEMZUQURUSCDULUMUNURUSNUOABEFGOPQUPULUNUMUOKZUTVBNZUMUNUORULESJZVDVEETZB
      ACDEFGHUAUBUCULUPUNVAFJZLVBVCNZUQUNVHULUMUNUOUDULUMUOVHUNULVFUMUOVHVGACDE
      FGHUEUFUGUHULUNVHVIBVAEFGOUIUJUK $.
      $( [15-Feb-2008] $)

    $( Law for double group division. $)
    abldivdiv $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
    ( A D ( B D C ) ) = ( ( A D B ) G C ) ) $=
      ( cabl wcel w3a wa co cgr wceq ablogrpo grpdivdiv sylan 3ancomb
      grpmuldivass ablmuldiv eqtr3d sylan2b eqtrd ) EIJZAFJZBFJZCFJZKZLABCDMDMZ
      ACBDMEMZABDMCEMZUEENJZUIUJUKOEPZABCDEFGHQRUIUEUFUHUGKZUKULOUFUGUHSUEUOLAC
      EMBDMZUKULUEUMUOUPUKOUNACBDEFGHTRACBDEFGHUAUBUCUD $.
      $( [29-Feb-2008] $)

    $( Law for double group division. $)
    abldivdiv4 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
    ( ( A D B ) D C ) = ( A D ( B G C ) ) ) $=
      ( cabl wcel w3a wa co cgn cfv cgr wceq ablogrpo simpl grpdivcl 3adant3r3
      simpr3 eqid grpdivval syl111anc sylan simpr1 simpr2 simp3 grpoinvcl
      syl2an 3jca abldivdiv syldan grpdivinv syl3an1 3adant3r1 opreq2d 3eqtr2d
      ) EIJZAFJZBFJZCFJZKZLZABDMZCDMZVFCENOZOZEMZABVIDMZDMZABCEMZDMUTEPJZVDVGVJ
      QZERZVNVDLVNVFFJZVCVOVNVDSVNVAVBVQVCABDEFGHTUAVNVAVBVCUBVFCDEVHFGVHUCZHUD
      UEUFUTVDVAVBVIFJZKVLVJQVEVAVBVSUTVAVBVCUGUTVAVBVCUHUTVNVCVSVDVPVAVBVCUICE
      VHFGVRUJUKULABVIDEFGHUMUNVEVKVMADUTVBVCVKVMQZVAUTVNVBVCVTVPBCDEVHFGVRHUOU
      PUQURUS $.
      $( [29-Feb-2008] $)

    $( Swap the second and third terms in a double division. $)
    abldiv23 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
   ( ( A D B ) D C ) = ( ( A D C ) D B ) ) $=
      ( cabl wcel w3a wa co wceq ablocom 3adant3r1 opreq2d abldivdiv4 3ancomb
      sylan2b 3eqtr4d ) EIJZAFJZBFJZCFJZKZLZABCEMZDMACBEMZDMZABDMCDMACDMBDMZUGU
      HUIADUBUDUEUHUINUCBCEFGOPQABCDEFGHRUFUBUCUEUDKUKUJNUCUDUESACBDEFGHRTUA $.
      $( [29-Feb-2008] $)

    $( Group theory analog of ~ nnncan . $)
    ablnnncan $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
      ( ( A D ( B D C ) ) D C ) = ( A D B ) ) $=
      ( cabl wcel w3a wa co wceq simpr1 cgr ablogrpo grpdivcl syl3an1
      3adant3r1 simpr3 3jca abldivdiv4 syldan grpnpcan opreq2d eqtrd ) EIJZAFJZ
      BFJZCFJZKZLZABCDMZDMCDMZAUNCEMZDMZABDMUHULUIUNFJZUKKUOUQNUMUIURUKUHUIUJUK
      OUHUJUKURUIUHEPJZUJUKUREQZBCDEFGHRSTUHUIUJUKUAUBAUNCDEFGHUCUDUMUPBADUHUJU
      KUPBNZUIUHUSUJUKVAUTBCDEFGHUESTUFUG $.
      $( [29-Feb-2008] $)

    $( Group theory analog of ~ nncan . $)
    ablnncan $p |- ( ( G e. AbelOp /\ A e. X /\ B e. X ) ->
             ( A D ( A D B ) ) = B ) $=
      ( cabl wcel w3a co cgi cfv wceq wa id 3anidm12 abldivdiv sylan2 3impb
      cgr ablogrpo eqid grpdivid sylan 3adant3 opreq1d grpolid 3adant2 3eqtrd
      ) DHIZAEIZBEIZJZAABCKCKZAACKZBDKZDLMZBDKZBUKULUMUOUQNZULUMOUKULULUMJZUTUL
      UMVAVAPQAABCDEFGRSTUNUPURBDUKULUPURNZUMUKDUAIZULVBDUBZACURDEFGURUCZUDUEUF
      UGUKUMUSBNZULUKVCUMVFVDBURDEFVEUHUEUIUJ $.
      $( [7-Mar-2008] $)

    $( Group theory analog of ~ nnncan1 . $)
    ablnnncan1 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
         ( ( A D B ) D ( A D C ) ) = ( C D B ) ) $=
      ( cabl wcel w3a wa co wceq simpr1 simpr2 cgr ablogrpo grpdivcl syl3an1
      3adant3r2 3jca abldiv23 syldan ablnncan opreq1d eqtrd ) EIJZAFJZBFJZCFJZK
      ZLZABDMACDMZDMZAUNDMZBDMZCBDMUHULUIUJUNFJZKUOUQNUMUIUJURUHUIUJUKOUHUIUJUK
      PUHUIUKURUJUHEQJUIUKURERACDEFGHSTUAUBABUNDEFGHUCUDUMUPCBDUHUIUKUPCNUJACDE
      FGHUEUAUFUG $.
      $( [7-Mar-2008] $)
  $}

  ${
    $d A k m $.  $d B k m $.  $d G k m $.  $d K m $.  $d P k m $.  $d X k m $.
    gxdi.1 $e |- X = ran G $.
    gxdi.2 $e |- P = ( ^g ` G ) $.
    $( Distribution of group power over group operation for abelian groups.
       (Contributed by Paul Chapman, 17-Apr-2009.) $)
    gxdi $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X ) /\ K e. ZZ ) ->
                 ( ( A G B ) P K ) = ( ( A P K ) G ( B P K ) ) ) $=
      ( vm vk cabl wcel wa cz co wceq wi cv cc0 cneg c1 caddc w3a opreq2
      opreq12d eqeq12d cgi cfv cgr ablogrpo 3ad2ant1 eqid grpoidcl syl grpolid
      syl11anc simp2 gx0 simp3 grpocl syl3an1 3eqtr4rd cn0 nn0z gxsuc
      syl111anc opreq1 3ad2ant3 simp1 gxcl abl4 syl122anc 3eqtrd eqtr4d 3expia
      ex syl5 cn nnz cgn gxneg ablocom wb eqeq1 mpbird fveq2d grpinvop zindd
      3expb 3impia ) DKLZAFLZBFLZMENLZABDOZECOZAECOZBECOZDOZPZWKWLWMWNWTQWOIRZC
      OZAXACOZBXACOZDOZPWOSCOZASCOZBSCOZDOZPWOJRZCOZAXJCOZBXJCOZDOZPZWOXJTZCOZA
      XPCOZBXPCOZDOZPZWOXJUAUBOZCOZAYBCOZBYBCOZDOZPZWTWKWLWMUCZIJEXASPZXBXFXEXI
      XASWOCUDYIXCXGXDXHDXASACUDXASBCUDUEUFXAXJPZXBXKXEXNXAXJWOCUDYJXCXLXDXMDXA
      XJACUDXAXJBCUDUEUFXAYBPZXBYCXEYFXAYBWOCUDYKXCYDXDYEDXAYBACUDXAYBBCUDUEUFX
      AXPPZXBXQXEXTXAXPWOCUDYLXCXRXDXSDXAXPACUDXAXPBCUDUEUFXAEPZXBWPXEWSXAEWOCU
      DYMXCWQXDWRDXAEACUDXAEBCUDUEUFYHDUGUHZYNDOZYNXIXFYHDUILZYNFLZYOYNPWKWLYPW
      MDUJZUKZYHYPYQYSYNDFGYNULZUMUNYNYNDFGYTUOUPYHXGYNXHYNDYHYPWLXGYNPYSWKWLWM
      UQZACYNDFGYTHURUPYHYPWMXHYNPYSWKWLWMUSZBCYNDFGYTHURUPUEYHYPWOFLZXFYNPYSWK
      YPWLWMUUCYRABDFGUTVAZWOCYNDFGYTHURUPVBXJVCLXJNLZYHXOYGQZXJVDYHUUEUUFYHUUE
      XOYGYHUUEXOUCZYCXLADOZXMBDOZDOZYFUUGYCXKWODOZXNWODOZUUJUUGYPUUCUUEYCUUKPY
      HUUEYPXOYSUKZYHUUEUUCXOUUDUKZYHUUEXOUQZWOCDXJFGHVEVFXOYHUUKUULPUUEXKXNWOD
      VGVHUUGWKXLFLZXMFLZWLWMUULUUJPYHUUEWKXOWKWLWMVIUKZUUGYPWLUUEUUPUUMYHUUEWL
      XOUUAUKZUUOACDXJFGHVJVFZUUGYPWMUUEUUQUUMYHUUEWMXOUUBUKZUUOBCDXJFGHVJVFZUU
      SUVAXLXMABDFGVKVLVMUUGYDUUHYEUUIDUUGYPWLUUEYDUUHPUUMUUSUUOACDXJFGHVEVFUUG
      YPWMUUEYEUUIPUUMUVAUUOBCDXJFGHVEVFUEVNVOVPVQXJVRLUUEYHXOYAQZXJVSYHUUEUVCY
      HUUEXOYAUUGXQXLDVTUHZUHZXMUVDUHZDOZXTUUGXQXKUVDUHZXMXLDOZUVDUHZUVGUUGYPUU
      CUUEXQUVHPUUMUUNUUOWOCDXJUVDFGUVDULZHWAVFUUGXKUVIUVDUUGXKUVIPZXNUVIPZUUGW
      KUUPUUQUVMUURUUTUVBXLXMDFGWBVFXOYHUVLUVMWCUUEXKXNUVIWDVHWEWFUUGYPUUQUUPUV
      JUVGPUUMUVBUUTXMXLDUVDFGUVKWGVFVMUUGXRUVEXSUVFDUUGYPWLUUEXRUVEPUUMUUSUUOA
      CDXJUVDFGUVKHWAVFUUGYPWMUUEXSUVFPUUMUVAUUOBCDXJUVDFGUVKHWAVFUEVNVOVPVQWHW
      IWJ $.
      $( [17-Apr-2009] $)
  $}

