$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Definition and basic properties of Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Abel $.

  $( Extend class notation with the class of all Abelian group operations. $)
  cabl $a class Abel $.

  ${
    $d g x y $.
    $( Define the class of all Abelian group operations. $)
    df-abl $a |- Abel = { g e. Grp | A. x e. ran g A. y e. ran g ( x g y ) =
                 ( y g x ) } $.
  $}

  ${
    $d g x y G $.  $d g x y X $.
    isabl.1 $e |- X = ran G $.
    $( The predicate "is an Abelian (commutative) group operation." $)
    isabl $p |- ( G e. Abel <-> ( G e. Grp /\
             A. x e. X A. y e. X ( x G y ) = ( y G x ) ) ) $=
      ( vg cv co wceq crn wral cgr cabl wb rneq syl6eqr raleq1 raleqd syl
      opreq eqeq12d 2ralbidv bitrd df-abl elrab2 ) AGZBGZFGZHZUGUFUHHZIZBUHJZKZ
      AULKZUFUGCHZUGUFCHZIZBDKADKZFCLMUHCIZUNUKBDKZADKZURUSULDIUNVANUSULCJDUHCO
      EPUMUTAULDUKBULDQRSUSUKUQABDDUSUIUOUJUPUFUGUHCTUGUFUHCTUAUBUCABFUDUE $.
      $( [4-Nov-2006] $) $( [2-Nov-2006] $)
  $}

  ${
    $d x y G $.
    $( An Abelian group operation is a group operation. $)
    ablgrp $p |- ( G e. Abel -> G e. Grp ) $=
      ( vx vy cabl wcel cgr cv co wceq crn wral eqid isabl pm3.26bi ) ADEAFEBGZ
      CGZAHPOAHICAJZKBQKBCAQQLMN $.
      $( [6-Nov-2006] $) $( [2-Nov-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y G $.  $d x y X $.
    ablcom.1 $e |- X = ran G $.
    $( An Abelian group operation is commutative. $)
    ablcom $p |- ( ( G e. Abel /\ A e. X /\ B e. X ) ->
                 ( A G B ) = ( B G A ) ) $=
      ( vx vy cabl wcel co wceq wa cv wral opreq1 opreq2 eqeq12d rcla42v cgr
      isabl pm3.27bi syl5com 3impib ) CHIZADIZBDIZABCJZBACJZKZUEUFLFMZGMZCJZUKU
      JCJZKZGDNFDNZUIUDUNUIAUKCJZUKACJZKFGABDDUJAKULUPUMUQUJAUKCOUJAUKCPQUKBKUP
      UGUQUHUKBACPUKBACOQRUDCSIUOFGCDETUAUBUC $.
      $( [10-Nov-2006] $) $( [2-Nov-2006] $)

    $( Commutative/associative law for Abelian groups. $)
    abl23 $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cabl wcel w3a wa co wceq ablcom 3adant3r1 opreq2d cgr grpass ablgrp
      sylan 3ancomb sylan2b 3eqtr4d ) DGHZAEHZBEHZCEHZIZJZABCDKZDKZACBDKZDKZABD
      KCDKZACDKBDKZUHUIUKADUCUEUFUIUKLUDBCDEFMNODPHZUGUMUJLUCABCDEFQDRZSUOUGUNU
      LLZUCUOUDUFUEIUQUGACBDEFQUDUEUFTUAUPSUB $.
      $( [26-Apr-2007] $) $( [26-Apr-2007] $)

    $( Commutative/associative law for Abelian groups. $)
    abl4 $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) )
               -> ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( cabl wcel wa co wceq w3a simprll simprlr simprrl 3jca abl23 syldan
      opreq1d cgr grpcl 3expb adantrr simprrr grpass ablgrp sylan adantrlr
      adantrrr 3eqtr3d 3impb ) EHIZAFIZBFIZJZCFIZDFIZJZABEKZCDEKEKZACEKZBDEKEKZ
      LUMUPUSJZJZUTCEKZDEKZVBBEKZDEKZVAVCVEVFVHDEUMVDUNUOUQMVFVHLVEUNUOUQUMUNUO
      USNUMUNUOUSOUMUPUQURPQABCEFGRSTEUAIZVDVGVALZUMVJVDUTFIZUQURMVKVJVDJZVLUQU
      RVJUPVLUSVJUNUOVLABEFGUBUCUDVJUPUQURPVJUPUQURUEZQUTCDEFGUFSEUGZUHVJVDVIVC
      LZUMVJVDVBFIZUOURMVPVMVQUOURVJUPUQVQURVJUNUQVQUOVJUNUQVQACEFGUBUCUIUJVJUN
      UOUSOVNQVBBDEFGUFSVOUHUKUL $.
      $( [2-May-2007] $) $( [26-Apr-2007] $)
  $}

  ${
    $d x y G $.  $d x y X $.
    isabli.1 $e |- G e. Grp $.
    isabli.2 $e |- dom G = ( X X. X ) $.
    isabli.3 $e |- ( ( x e. X /\ y e. X ) -> ( x G y ) = ( y G x ) ) $.
    $( Properties that determine an Abelian group operation. $)
    isabli $p |- G e. Abel $=
      ( cabl wcel cgr cv co wceq wral grprn isabl rgen2a mpbir2an ) CHICJIAKZBK
      ZCLTSCLMZBDNADNABCDCDEFOPEUAABDGQR $.
      $( [9-Apr-2008] $) $( [5-Nov-2006] $)
  $}

  ${
    abldiv.1 $e |- X = ran G $.
    abldiv.3 $e |- D = ( /g ` G ) $.
    $( Law for group multiplication and division. $)
    ablmuldiv $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
     ( ( A G B ) D C ) = ( ( A D C ) G B ) ) $=
      ( cabl wcel w3a wa co wceq ablcom 3adant3r3 opreq1d cgr grpmuldivass
      ablgrp sylan 3ancoma sylan2b 3simpr2 grpdivcl syl3an1 3adant3r2 jca
      3expb syldan 3eqtrd ) EIJZAFJZBFJZCFJZKZLZABEMZCDMBAEMZCDMZBACDMZEMZVABEM
      ZUQURUSCDULUMUNURUSNUOABEFGOPQULUNUMUOKZUTVBNZUPERJZVDVEULBACDEFGHSETZUAU
      MUNUOUBUCULUPUNVAFJZLVBVCNZUQUNVHULUMUNUOUDULUMUOVHUNVFUMUOVHULACDEFGHUEV
      GUFUGUHULUNVHVIBVAEFGOUIUJUK $.
      $( [25-Feb-2008] $) $( [15-Feb-2008] $)

    $( Law for double group division. $)
    abldivdiv $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
    ( A D ( B D C ) ) = ( ( A D B ) G C ) ) $=
      ( cabl wcel w3a wa co cgr wceq grpdivdiv ablgrp sylan grpmuldivass
      ablmuldiv eqtr3d 3ancomb sylan2b eqtrd ) EIJZAFJZBFJZCFJZKZLABCDMDMZACBDM
      EMZABDMCEMZENJZUIUJUKOUEABCDEFGHPEQZRUEUFUHUGKZUKULOUIUEUOLACEMBDMZUKULUM
      UOUPUKOUEACBDEFGHSUNRACBDEFGHTUAUFUGUHUBUCUD $.
      $( [29-Feb-2008] $) $( [29-Feb-2008] $)

    $( Law for double group division. $)
    abldivdiv4 $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
    ( ( A D B ) D C ) = ( A D ( B G C ) ) ) $=
      ( cabl wcel w3a wa co cgn cfv cgr wceq eqid grpdivval pm3.26 grpdivcl
      3adant3r3 3simpr3 syl3anc ablgrp sylan 3simpr1 3simpr2 grpinvcl 3simp3
      syl2an 3jca abldivdiv syldan grpdivinv syl3an1 3adant3r1 opreq2d 3eqtr2d
      ) EIJZAFJZBFJZCFJZKZLZABDMZCDMZVFCENOZOZEMZABVIDMZDMZABCEMZDMEPJZVDVGVJQZ
      UTVNVFFJZVCVOVNVDLVFCDEVHFGVHRZHSVNVDTVNVAVBVPVCABDEFGHUAUBVNVAVBVCUCUDEU
      EZUFUTVDVAVBVIFJZKVLVJQVEVAVBVSUTVAVBVCUGUTVAVBVCUHVNVCVSUTVDCEVHFGVQUIVR
      VAVBVCUJUKULABVIDEFGHUMUNVEVKVMADUTVBVCVKVMQZVAVNVBVCVTUTBCDEVHFGVQHUOVRU
      PUQURUS $.
      $( [29-Feb-2008] $) $( [29-Feb-2008] $)

    $( Swap the second and third terms in a double division. $)
    abldiv23 $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
   ( ( A D B ) D C ) = ( ( A D C ) D B ) ) $=
      ( cabl wcel w3a wa co wceq ablcom 3adant3r1 opreq2d abldivdiv4 3ancomb
      sylan2b 3eqtr4d ) EIJZAFJZBFJZCFJZKZLZABCEMZDMACBEMZDMZABDMCDMACDMBDMZUGU
      HUIADUBUDUEUHUINUCBCEFGOPQABCDEFGHRUBUCUEUDKUKUJNUFACBDEFGHRUCUDUESTUA $.
      $( [29-Feb-2008] $) $( [29-Feb-2008] $)

    $( Group theory analog of ~ nnncan . $)
    ablnnncan $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
      ( ( A D ( B D C ) ) D C ) = ( A D B ) ) $=
      ( cabl wcel w3a wa co wceq 3simpr1 cgr grpdivcl ablgrp syl3an1 3adant3r1
      3simpr3 3jca abldivdiv4 syldan grpnpcan opreq2d eqtrd ) EIJZAFJZBFJZCFJZK
      ZLZABCDMZDMCDMZAUNCEMZDMZABDMUHULUIUNFJZUKKUOUQNUMUIURUKUHUIUJUKOUHUJUKUR
      UIEPJZUJUKURUHBCDEFGHQERZSTUHUIUJUKUAUBAUNCDEFGHUCUDUMUPBADUHUJUKUPBNZUIU
      SUJUKVAUHBCDEFGHUEUTSTUFUG $.
      $( [29-Feb-2008] $) $( [29-Feb-2008] $)

    $( Group theory analog of ~ nncan . $)
    ablnncan $p |- ( ( G e. Abel /\ A e. X /\ B e. X ) ->
             ( A D ( A D B ) ) = B ) $=
      ( cabl wcel w3a co cgi cfv wceq wa abldivdiv id 3anidm12 sylan2 3impb
      cgr eqid grpdivid ablgrp sylan 3adant3 opreq1d grplid 3adant2 3eqtrd ) DH
      IZAEIZBEIZJZAABCKCKZAACKZBDKZDLMZBDKZBUKULUMUOUQNZUKULULUMJZUTULUMOAABCDE
      FGPULUMVAVAQRSTUNUPURBDUKULUPURNZUMDUAIZULVBUKACURDEFGURUBZUCDUDZUEUFUGUK
      UMUSBNZULVCUMVFUKBURDEFVDUHVEUEUIUJ $.
      $( [7-Mar-2008] $) $( [7-Mar-2008] $)

    $( Group theory analog of ~ nnncan1 . $)
    ablnnncan1 $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
         ( ( A D B ) D ( A D C ) ) = ( C D B ) ) $=
      ( cabl wcel w3a wa co wceq 3simpr1 3simpr2 cgr grpdivcl ablgrp syl3an1
      3adant3r2 3jca abldiv23 syldan ablnncan opreq1d eqtrd ) EIJZAFJZBFJZCFJZK
      ZLZABDMACDMZDMZAUNDMZBDMZCBDMUHULUIUJUNFJZKUOUQNUMUIUJURUHUIUJUKOUHUIUJUK
      PUHUIUKURUJEQJUIUKURUHACDEFGHRESTUAUBABUNDEFGHUCUDUMUPCBDUHUIUKUPCNUJACDE
      FGHUEUAUFUG $.
      $( [2-Apr-2008] $) $( [7-Mar-2008] $)
  $}

  ${
    $d A k m $.  $d B k m $.  $d G k m $.  $d K m $.  $d P k m $.  $d X k m $.
    gxdi.1 $e |- X = ran G $.
    gxdi.2 $e |- P = ( ^g ` G ) $.
    $( Distribution of group power over group operation for abelian groups.
       (Contributed by Paul Chapman, 17-Apr-2009.) $)
    gxdi $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X ) /\ K e. ZZ ) ->
                 ( ( A G B ) P K ) = ( ( A P K ) G ( B P K ) ) ) $=
      ( vm vk cabl wcel wa cz co wceq wi cv cc0 cneg c1 caddc w3a opreq2
      opreq12d eqeq12d cgi cfv cgr eqid grplid ablgrp 3ad2ant1 grpidcl syl
      sylanc gx0 3simp2 3simp3 grpcl syl3an1 3eqtr4rd cn0 gxsuc syl3anc opreq1
      3ad2ant3 abl4 3simp1 gxcl jca 3eqtrd eqtr4d 3expia ex nn0z syl5 cn cgn
      gxneg ablcom wb eqeq1 mpbird fveq2d grpinvop nnz zindd 3expb 3impia ) DKL
      ZAFLZBFLZMZENLZABDOZECOZAECOZBECOZDOZPZWKWLWMWOXAQWPIRZCOZAXBCOZBXBCOZDOZ
      PWPSCOZASCOZBSCOZDOZPWPJRZCOZAXKCOZBXKCOZDOZPZWPXKTZCOZAXQCOZBXQCOZDOZPZW
      PXKUAUBOZCOZAYCCOZBYCCOZDOZPZXAWKWLWMUCZIJEXBSPZXCXGXFXJXBSWPCUDYJXDXHXEX
      IDXBSACUDXBSBCUDUEUFXBXKPZXCXLXFXOXBXKWPCUDYKXDXMXEXNDXBXKACUDXBXKBCUDUEU
      FXBYCPZXCYDXFYGXBYCWPCUDYLXDYEXEYFDXBYCACUDXBYCBCUDUEUFXBXQPZXCXRXFYAXBXQ
      WPCUDYMXDXSXEXTDXBXQACUDXBXQBCUDUEUFXBEPZXCWQXFWTXBEWPCUDYNXDWRXEWSDXBEAC
      UDXBEBCUDUEUFYIDUGUHZYODOZYOXJXGDUILZYOFLZYPYOPYIYOYODFGYOUJZUKWKWLYQWMDU
      LZUMZYIYQYRUUAYODFGYSUNUOUPYIXHYOXIYODYQWLXHYOPYIACYODFGYSHUQUUAWKWLWMURZ
      UPYQWMXIYOPYIBCYODFGYSHUQUUAWKWLWMUSZUPUEYQWPFLZXGYOPYIWPCYODFGYSHUQUUAYQ
      WLWMUUDWKABDFGUTYTVAZUPVBYIXKNLZXPYHQZXKVCLYIUUFUUGYIUUFXPYHYIUUFXPUCZYDX
      MADOZXNBDOZDOZYGUUHYDXLWPDOZXOWPDOZUUKYQUUDUUFYDUULPUUHWPCDXKFGHVDYIUUFYQ
      XPUUAUMZYIUUFUUDXPUUEUMZYIUUFXPURZVEXPYIUULUUMPUUFXLXOWPDVFVGWKXMFLZXNFLZ
      MWNUUMUUKPUUHXMXNABDFGVHYIUUFWKXPWKWLWMVIUMZUUHUUQUURYQWLUUFUUQUUHACDXKFG
      HVJUUNYIUUFWLXPUUBUMZUUPVEZYQWMUUFUURUUHBCDXKFGHVJUUNYIUUFWMXPUUCUMZUUPVE
      ZVKUUHWLWMUUTUVBVKVEVLUUHYEUUIYFUUJDYQWLUUFYEUUIPUUHACDXKFGHVDUUNUUTUUPVE
      YQWMUUFYFUUJPUUHBCDXKFGHVDUUNUVBUUPVEUEVMVNVOXKVPVQYIUUFXPYBQZXKVRLYIUUFU
      VDYIUUFXPYBUUHXRXMDVSUHZUHZXNUVEUHZDOZYAUUHXRXLUVEUHZXNXMDOZUVEUHZUVHYQUU
      DUUFXRUVIPUUHWPCDXKUVEFGUVEUJZHVTUUNUUOUUPVEUUHXLUVJUVEUUHXLUVJPZXOUVJPZW
      KUUQUURUVNUUHXMXNDFGWAUUSUVAUVCVEXPYIUVMUVNWBUUFXLXOUVJWCVGWDWEYQUURUUQUV
      KUVHPUUHXNXMDUVEFGUVLWFUUNUVCUVAVEVLUUHXSUVFXTUVGDYQWLUUFXSUVFPUUHACDXKUV
      EFGUVLHVTUUNUUTUUPVEYQWMUUFXTUVGPUUHBCDXKUVEFGUVLHVTUUNUVBUUPVEUEVMVNVOXK
      WGVQWHWIWJ $.
      $( [20-Apr-2009] $) $( [17-Apr-2009] $)
  $}

