$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Group actions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c GrpAct $.
  $( Extend class definition to include the class of group actions. $)
  cga $a class GrpAct $.

  ${
    $d g m s x y z $.
    $( Define the class of all group actions. $)
    df-ga $a |- GrpAct = { <. g , m >. | ( g e. Grp /\ E. s ( m : ( ran g X. s
    ) --> s /\ A. x e. s ( ( ( Id ` g ) m x ) = x /\ A. y e. ran g A. z e. ran
    g ( ( y g z ) m x ) = ( y m ( z m x ) ) ) ) ) } $.
  $}

  ${
    $d g m s x y z G $.  $d g m s x y z M $.  $d g m U $.  $d g m y z X $.
    isgalem.1 $e |- X = ran G $.
    isgalem.2 $e |- U = ( Id ` G ) $.
    $( Lemma for ~ isga . $)
    isgalem $p |- ( M e. A -> ( <. G , M >. e. GrpAct <-> ( G e. Grp /\ E. s (
    M : ( X X. s ) --> s /\ A. x e. s ( ( U M x ) = x /\ A. y e. X A. z e. X (
    ( y G z ) M x ) = ( y M ( z M x ) ) ) ) ) ) ) $=
      ( vg vm wcel cop cga cgr cv cxp wf co wceq wral wa wex cvv wbr df-br
      wrel crn cgi cfv copab relopab df-ga releqi mpbir brrelexi sylbir anim1i
      ancoms elisset adantr eleq1 wb rneq syl6eqr xpeq1 feq2 3syl fveq2
      opreq1d eqeq1d opreq raleqbidv anbi12d ralbidv exbidv feq1 opreq2d eqtrd
      eqeq12d 2ralbidv anbi2d opelopabg eleq2i syl5bb pm5.21nd ) GDNZFGOZPNZFQN
      ZHIRZSZWMGTZEARZGUAZWPUBZBRZCRZFUAZWPGUAZWSWTWPGUAZGUAZUBZCHUCBHUCZUDZAWM
      UCZUDZIUEZUDZFUFNZWIUDZWKWIXMWKXLWIWKFGPUGXLFGPUHFGPPUILRZQNZXNUJZWMSZWMM
      RZTZXNUKULZWPXRUAZWPUBZWSWTXNUAZWPXRUAZWSWTWPXRUAZXRUAZUBZCXPUCZBXPUCZUDZ
      AWMUCZUDZIUEZUDZLMUMZUIYNLMUNPYOABCLMIUOZUPUQURUSUTVAXKWIXMXKXLWIWLXLXJFQ
      VBVCUTVAXMWJYONXKWKYNWLWNWMXRTZEWPXRUAZWPUBZXAWPXRUAZYFUBZCHUCZBHUCZUDZAW
      MUCZUDZIUEZUDXKLMFGUFDXNFUBZXOWLYMUUGXNFQVDUUHYLUUFIUUHXSYQYKUUEUUHXPHUBX
      QWNUBXSYQVEUUHXPFUJHXNFVFJVGZXPHWMVHXQWNWMXRVIVJUUHYJUUDAWMUUHYBYSYIUUCUU
      HYAYRWPUUHXTEWPXRUUHXTFUKULEXNFUKVKKVGVLVMUUHYHUUBBXPHUUIUUHYGUUACXPHUUIU
      UHYDYTYFUUHYCXAWPXRWSWTXNFVNVLVMVOVOVPVQVPVRVPXRGUBZUUGXJWLUUJUUFXIIUUJYQ
      WOUUEXHWNWMXRGVSUUJUUDXGAWMUUJYSWRUUCXFUUJYRWQWPEWPXRGVNVMUUJUUAXEBCHHUUJ
      YTXBYFXDXAWPXRGVNUUJYFWSYEGUAXDWSYEXRGVNUUJYEXCWSGWTWPXRGVNVTWAWBWCVPVQVP
      VRWDWEPYOWJYPWFWGWH $.
      $( [17-May-2010] $) $( [10-Aug-2009] $)
  $}

  ${
    $d s A $.  $d s x y z G $.  $d s x y z M $.  $d s U $.  $d s y z X $.
    $d s x Y $.
    isga.1 $e |- X = ran G $.
    isga.2 $e |- Y = ran dom M $.
    isga.3 $e |- U = ( Id ` G ) $.
    $( The predicate "is a (left) group action."  The group ` G ` is said to
       act on the base set ` Y ` of the action, which is not assumed to have
       any special properties.  There is a related notion of right group
       action, but as the Wikipedia article explains, it is not mathematically
       interesting.  The way actions are usually thought of is that each
       element ` g ` of ` G ` is a permutation of the elements of ` Y ` (see
       ~ gapm ).  Since group theory was classically about symmetry groups, it
       is therefore likely that the notion of group action was useful even in
       early group theory.  (Contributed by Jeff Hankins, 10-Aug-2009.) $)
    isga $p |- ( M e. A -> ( <. G , M >. e. GrpAct <-> ( G e. Grp /\ M : ( X X.
    Y ) --> Y /\ A. x e. Y ( ( U M x ) = x /\ A. y e. X A. z e. X ( ( y G z ) M
    x ) = ( y M ( z M x ) ) ) ) ) ) $=
      ( vs wcel cop cga cgr cv cxp wf co wceq wral wa wex w3a isgalem wi cdm
      crn fdm adantr 3ad2ant1 rneqd c0 wne grpn0 3ad2ant2 rnxp syl eqtr2d
      syl6eqr wb xpeq2 feq23 mpancom raleq 3anbi23d biimpcd 3expb ancoms
      3adant3 mpd 3exp 19.23aiv impcom com12 cvv dmexg rnexg syl5eqel anbi12d
      cla4egv 3syl anim2d 3impb impbid bitrd ) GDNZFGOPNFQNZHMRZSZWKGTZEARZGUAW
      NUBBRZCRZFUAWNGUAWOWPWNGUAGUAUBCHUCBHUCUDZAWKUCZUDZMUEZUDZWJHISZIGTZWQAIU
      CZUFZABCDEFGHMJLUGWIXAXEXAWIXEWTWJWIXEUHZWSWJXFUHMWSWJWIXEWSWJWIUFZWKIUBZ
      XEXGWKGUIZUJZIXGXJWLUJZWKXGXIWLWSWJXIWLUBZWIWMXLWRWLWKGUKULUMUNXGHUOUPZXK
      WKUBWJWSXMWIFHJUQURHWKUSUTVAKVBWSWJXHXEUHZWIWJWSXNWJWMWRXNXHWJWMWRUFXEXHW
      MXCWRXDWJWLXBUBXHWMXCVCWKIHVDWLWKXBIGVEVFZWQAWKIVGZVHVIVJVKVLVMVNVOVPVQXE
      WIXAWJXCXDWIXAUHWIWJXCXDUDZUDXAWIXQWTWJWIXIVRNZIVRNXQWTUHGDVSXRXJVRIXIVRV
      TKWAWSXQMIVRXHWMXCWRXDXOXPWBWCWDWEVQWFVQWGWH $.
      $( [17-May-2010] $) $( [10-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z M $.  $d x y z X $.  $d x Y $.
    gafo.1 $e |- X = ran G $.
    gafo.2 $e |- Y = ran dom M $.
    $( A group action is onto its base set.  (Contributed by Jeff Hankins,
       10-Aug-2009.) $)
    gafo $p |- ( ( M e. A /\ <. G , M >. e. GrpAct ) -> M : ( X X. Y ) -onto->
    Y ) $=
      ( vx vy vz wcel cop cga wa cxp wfn crn wceq wfo cgr wf cgi cfv cv co
      wral w3a eqid isga biimpa ffn 3ad2ant2 wss frn wi wal ax-17 hbra1 hb3an
      ra4 pm3.26 adantr fnoprvrn adantl grpidcl 3ad2ant3 3simpl1 syl3anc
      eqeltrrd 3exp1 sylcom com4t 3imp 19.21ai dfss2 sylibr eqssd jca syl
      df-fo ) CAKZBCLMKZNZCDEOZPZCQZERZNZWDECSWCBTKZWDECUAZBUBUCZHUDZCUEZWLRZIU
      DZJUDZBUEWLCUEWOWPWLCUECUERJDUFIDUFZNZHEUFZUGZWHWAWBWTHIJAWKBCDEFGWKUHZUI
      UJWTWEWGWJWIWEWSWDECUKZULWTWFEWJWIWFEUMWSWDECUNULWTWLEKZWLWFKZUOZHUPEWFUM
      WTXEHWIWJWSHWIHUQWJHUQWRHEURUSWIWJWSXEWSXCWIWJXDWSXCWRWIWJXDUOUOWRHEUTXCW
      RWIWJXDXCWRWIUGZWJNZWMWLWFXFWNWJWRXCWNWIWNWQVAULVBWEWKDKZXCWMWFKXGDEWKWLC
      VCWJWEXFXBVDXFXHWJWIXCXHWRWKBDFXAVEVFVBXCWRWIWJVGVHVIVJVKVLVMVNHEWFVOVPVQ
      VRVSWDECVTVP $.
      $( [17-May-2010] $) $( [10-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z M $.  $d y z X $.  $d x Y $.
    isga2.1 $e |- X = ran G $.
    isga2.2 $e |- Y = ran M $.
    isga2.3 $e |- U = ( Id ` G ) $.
    $( An equivalent form of ~ isga .  (Contributed by Jeff Hankins,
       10-Aug-2009.) $)
    isga2 $p |- ( M e. A -> ( <. G , M >. e. GrpAct <-> ( G e. Grp /\ M : ( X
    X. Y ) --> Y /\ A. x e. Y ( ( U M x ) = x /\ A. y e. X A. z e. X ( ( y G z
    ) M x ) = ( y M ( z M x ) ) ) ) ) ) $=
      ( wcel cop cga cgr cxp wf cv co wceq wral wa w3a cdm crn pm3.26 wfo eqid
      gafo forn syl5eq syl jca fdm rneqd adantl c0 wne grpn0 rnxp adantr
      eqtr2d 3adant3 anim2i isga wb xpeq2 feq23 mpancom raleq 3anbi23d eqcoms
      sylan9bb pm5.21nd ) GDMZFGNOMZFPMZHIQZIGRZEASZGTWAUABSZCSZFTWAGTWBWCWAGTG
      TUACHUBBHUBUCZAIUBZUDZVPIGUEZUFZUAZUCVPVQUCZVPWIVPVQUGWJHWHQZWHGUHZWIDFGH
      WHJWHUIZUJWLGUFWHIWKWHGUKKULUMUNWFWIVPVRVTWIWEVRVTUCWHVSUFZIVTWHWNUAVRVTW
      GVSVSIGUOUPUQVRWNIUAZVTVRHURUSWOFHJUTHIVAUMVBVCVDVEVPVQVRWKWHGRZWDAWHUBZU
      DZWIWFABCDEFGHWHJWMLVFWRWFVGWHIWHIUAZWPVTWQWEVRWKVSUAWSWPVTVGWHIHVHWKWHVS
      IGVIVJWDAWHIVKVLVMVNVO $.
      $( [17-May-2010] $) $( [10-Aug-2009] $)
  $}

  ${
    $d x y z G $.
    $( The action of a group on the empty set.  (Contributed by Jeff Hankins,
       11-Aug-2009.) $)
    ga0 $p |- ( G e. Grp -> <. G , (/) >. e. GrpAct ) $=
      ( vx vy vz cgr wcel crn c0 cxp wf cgi cfv cv co wceq wral wa w3a cop cga
      id f00 eqid xp0 mpbir2an a1i ral0 3jca cvv wb 0ex rn0 eqcomi isga2 ax-mp
      sylibr ) AEFZUQAGZHIZHHJZAKLZBMZHNVBOCMZDMZANVBHNVCVDVBHNHNODURPCURPQZBHP
      ZRZAHSTFZUQUQUTVFUQUAUTUQUTHHOUSHOUSHUBHUCURUDUEUFVFUQVEBUGUFUHHUIFVHVGUJ
      UKBCDUIVAAHURHURUCHGHULUMVAUCUNUOUP $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z G $.  $d x y z S $.  $d x y z X $.
    gaid.1 $e |- X = ran G $.
    $( The trivial action of a group on any set.  Each group element
       corresponds to the identity permutation.  (Contributed by Jeff Hankins,
       11-Aug-2009.) $)
    gaid $p |- ( ( G e. Grp /\ S e. A ) -> <. G , ( 2nd |` ( X X. S ) ) >. e.
    GrpAct ) $=
      ( vx vy vz cgr wcel wa c2nd cxp cres cop cga crn wf cgi cfv cv co wceq
      wral w3a pm3.26 f2ndres a1i opelxpi eqid grpidcl 3ad2ant1 3simp3 sylanc
      fvres syl df-opr syl5eq fvex visset op2nd syl6eq wi grpcl 3exp 3imp
      syl6req syl6eqr oprex 3eqtr4g 3simp2 syl5eqel eqeltrd syl5req 3eqtrd
      r19.21adv r19.21aiv jca 3expia 3jca wb c0 wne wfo grpn0 fo2ndres forn
      3syl adantr xpeq2 feq23 mpancom raleq 3anbi23d mpbird cvv fex xpexg
      rnexg sylan sylancr isga2 ) CIJZBAJZKZCLDBMZNZOPJZXCDXGQZMZXIXGRZCSTZFUAZ
      XGUBZXMUCZGUAZHUAZCUBZXMXGUBZXPXQXMXGUBZXGUBZUCZHDUDZGDUDZKZFXIUDZUEZXEYG
      XCXFBXGRZYEFBUDZUEZXEXCYHYIXCXDUFYHXEDBUGZUHXEYEFBXCXDXMBJZYEXCXDYLUEZXOY
      DYMXNXLXMOZLTZXMYMYNXGTZYOXNYMYNXFJZYPYOUCXLDJZYLYQYMXLXMDBUIXCXDYRYLXLCD
      EXLUJZUKULXCXDYLUMZUNYNXFLUOUPXLXMXGUQURXLXMCSUSFUTZVAVBYMYCGDYMXPDJZYBHD
      YMUUBXQDJZYBYMUUBUUCUEZXSXRXMOZLTZXPXTOZLTZYAUUDUUEXGTZUUFXSUUDUUEXFJZUUI
      UUFUCXRDJZYLUUJUUDXRXMDBUIYMUUBUUCUUKXCXDUUBUUCUUKVCVCYLXCUUBUUCUUKXPXQCD
      EVDVEULVFYMUUBYLUUCYTULZUNUUEXFLUOUPXRXMXGUQURUUDXMXTUUFUUHUUDXMXQXMOZXGT
      ZXTUUDUUNUUMLTZXMUUDUUMXFJZUUNUUOUCUUCYLUUPUUDXQXMDBUIYMUUBUUCUMUULUNUUMX
      FLUOUPZXQXMHUTUUAVAZVGXQXMXGUQZVHXRXMXPXQCVIUUAVAXPXTGUTXQXMXGVIVAVJUUDUU
      GXGTZUUHYAUUDUUGXFJZUUTUUHUCUUBXTBJUVAUUDXPXTDBUIYMUUBUUCVKUUDXTUUOBUUDUU
      NUUOXTUUQUUSURUUDXMBUUOUULUURVLVMUNUUGXFLUOUPXPXTXGUQVNVOVEVPVQVRVSVQVTXE
      XIBUCZYGYJWAXCUVBXDXCDWBWCXFBXGWDUVBCDEWEDBWFXFBXGWGWHWIUVBXKYHYFYIXCXJXF
      UCUVBXKYHWAXIBDWJXJXIXFBXGWKWLYEFXIBWMWNUPWOXEXGWPJZXHYGWAYHXFWPJZUVCXEXF
      BWPXGWQYKDWPJXDUVDXCDBWPAWRXCCQWPDCIWSEVLWTXAFGHWPXLCXGDXIEXIUJYSXBUPWO
      $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z H $.  $d x y z X $.  $d x y z Y $.
    ssga.1 $e |- X = ran G $.
    ssga.2 $e |- Y = ran H $.
    $( A subgroup acts on its parent group.  (Contributed by Jeff Hankins,
       13-Aug-2009.) $)
    ssga $p |- ( H e. ( SubGrp ` G ) -> <. H , ( G |` ( Y X. X ) ) >. e. GrpAct
    ) $=
      ( vx vy vz csubg cfv wcel cxp cres cop cga cgr crn wf cgi cv co wceq
      wral wa w3a wss issubg 3simp2bi wfn wfun cdm df-fn wfo 3simp1bi grpfo
      fofun funres syl 3syl subgrnss wex wi opelxpi ssel impcom adantlr simplr
      sylanc ex adantl wb eleq1 adantr sylibrd 19.23aivv com12 elxp syl5ib
      ssrdv fof fdm sseqtr4d ssdmres sylib sylanbrc resss rnss ax-mp sseqtr4i
      jctir df-f sylibr eqid grpidcl 3ad2ant2 sylbi sylan fvres subgid opreq1d
      grplid eqtrd df-opr syl5eqr syl5eq foprrn ad2antrr simprl simprr syl3anc
      subgopr simpll pm3.27 sylc ancoms adantll 3eqtr4g adantrl opreq2d
      ad2ant2l ssel2 ad2ant2rl sylanl1 syl5eqelr eqeltrd grpass ad2ant2r 3jca
      3eqtr4rd 3eqtrd r19.21aivv jca r19.21aiva cima imassrn a1i opeq1 fveq2d
      eqeq1d mpbird jca31 visset opelxp bicomi funopfvb sseldd eleqtrrd
      anbi12d mpbid opex eleq1d cla4ev syl6 elima3 syl6ibr eqssd df-ima xpeq2
      feq23 mpancom raleq 3anbi23d cvv resexg isga2 ) BAJKLZBADCMZNZOPLZBQLZDUV
      IRZMZUVLUVISZBTKZGUAZUVIUBZUVPUCZHUAZIUAZBUBZUVPUVIUBZUVSUVTUVPUVIUBZUVIU
      BZUCZIDUDHDUDZUEZGUVLUDZUFZUVGUWIUVKUVHCUVISZUWGGCUDZUFZUVGUVKUWJUWKUVGAQ
      LZUVKBAUGZABUHZUIZUVGUVIUVHUJZUVLCUGZUEUWJUVGUWQUWRUWQUVIUKZUVIULUVHUCZUV
      GUVIUVHUMUVGUWMCCMZCAUNZUWSUVGUWMUVKUWNUWOUOZACEUPZUXBAUKZUWSUXACAUQZUVHA
      URUSUTUVGUVHAULZUGUWTUVGUVHUXAUXGUVGDCUGZUVHUXAUGABDCEFVAZUXHGUVHUXAUXHUV
      PUVSUVTOZUCZUVSDLZUVTCLZUEZUEZIVBHVBZUVPUXALZUVPUVHLUXPUXHUXQUXOUXHUXQVCH
      IUXOUXHUXJUXALZUXQUXNUXHUXRVCUXKUXNUXHUXRUVSCLZUXMUXRUXNUXHUEUVSUVTCCVDUX
      LUXHUXSUXMUXHUXLUXSDCUVSVEVFVGUXLUXMUXHVHVIVJVKUXKUXQUXRVLUXNUVPUXJUXAVMV
      NVOVPVQHIUVPDCVRVSVTUSUVGUWMUXBUXGUXAUCZUXCUXDUXBUXACASZUXTUXACAWAZUXACAW
      BZUSUTWCUVHAWDWEWFUVLARZCUVIAUGUVLUYDUGAUVHWGUVIAWHWIEWJWKUVHCUVIWLWMUVGU
      WGGCUVGUVPCLZUEZUVRUWFUYFUVOUVPOZUVIKZUVPUVQUYFUYHUYGAKZUVPUYFUYGUVHLZUYH
      UYIUCUVODLZUYEUYJUVGUVOUVPDCVDUVGUWMUVKUWNUFZUYKUWOUVKUWMUYKUWNUVOBDFUVOW
      NZWOZWPWQWRUYGUVHAWSUSUYFUVOUVPAUBZUVPUYIUYFUYOATKZUVPAUBZUVPUYFUVOUYPUVP
      AUVGUVOUYPUCZUYEUVOUYPABUYPWNZUYMWTZVNXAUWMUYEUYQUVPUCUVGUVPUYPACEUYSXBZU
      XCWRXCUVOUVPAXDXEXCUVOUVPUVIXDXFUYFUWEHIDDUYFUXLUVTDLZUEZUWEUYFVUCUEZUWBU
      WAUVPOZAKZUVSUVTAUBZUVPAUBZUWDVUDVUEUVIKZVUFUWBVUDVUEUVHLZVUIVUFUCUWADLZU
      YEVUJVUDUWAUVPDCVDDDMZDBSZUXLVUBVUKVUDUVSUVTDDDBXGUVGVUMUYEVUCUVGUYLVUMUW
      OUVKUWMVUMUWNUVKVULDBUNVUMBDFUPVULDBWAUSWPWQXHUYFUXLVUBXIZUYFUXLVUBXJXKUV
      GUYEVUCVHZVIVUEUVHAWSUSUWAUVPUVIXDXFVUDUWAUVPAUBVUHVUFVUDUWAVUGUVPAUVGVUC
      UWAVUGUCVUDUVSUVTABDFXLUVGUYEVUCXMUYFVUCXNXOXAUWAUVPAXDXEVUDUVSUWCAUBZUVS
      UVTUVPAUBZAUBZUWDVUHVUDUWCVUQUVSAUYFVUBUWCVUQUCUXLUYFVUBUEZUVTUVPOZUVIKZV
      UTAKZUWCVUQVUSVUTUVHLZVVAVVBUCZUYEVUBVVCUVGVUBUYEVVCUVTUVPDCVDXPZXQVUTUVH
      AWSZUSUVTUVPUVIXDZUVTUVPAXDZXRXSXTVUDUVSUWCOZUVIKZVVIAKZUWDVUPVUDVVIUVHLZ
      VVJVVKUCUXLUWCCLVVLVUDUVSUWCDCVDVUNVUDUWCVVBCVUDVVAVVBUWCVUDVVCVVDUYEVUBV
      VCUVGUXLVVEYAVVFUSVVGXFVUDVUQCVVBUYAUXMUYEVUQCLVUDUVTUVPCCCAXGUVGUYAUYEVU
      CUVGUWMUXBUYAUXCUXDUYBUTXHUXHUYEVUCUXMUVGUXHVUBUXMUYEUXLDCUVTYBYCZUXIYDVU
      OXKVVHYEYFVIVVIUVHAWSUSUVSUWCUVIXDUVSUWCAXDXRUWMUXSUXMUYEUFZVUHVURUCVUDUV
      SUVTUVPACEYGUVGUWMUYEVUCUXCXHUXHUYEVUCVVNUVGUXHUYEUEVUCUEUXSUXMUYEUXHUXLU
      XSUYEVUBDCUVSYBYHVVMUXHUYEVUCVHYIUXIYDVIYJYKVJYLYMYNYIUVGUVLCUCZUWIUWLVLU
      VGAUVHYOZCUVLUVGVVPCVVPCUGUVGVVPUYDCAUVHYPEWJYQUVGGCVVPUVGUYEUVSUVHLZUVSU
      VPOZALZUEZHVBZUVPVVPLUVGUYEUYJUYGUVPOZALZUEZVWAUVGUYEVWDUYFUYKUYEUEZUYIUV
      PUCZUEVWDUYFUYKUYEVWFUVGUYKUYEUVGUVKUYKUWPUYNUSZVNUVGUYEXNUYFVWFUYPUVPOZA
      KZUVPUCZUWMUYEVWJUVGUWMUYEUEUYQUVPVWIVUAUYPUVPAXDXEUXCWRUVGVWFVWJVLZUYEUV
      GUYRVWKUYTUYRUYIVWIUVPUYRUYGVWHAUVOUYPUVPYRYSYTUSVNUUAUUBUYFVWEUYJVWFVWCV
      WEUYJVLUYFUYJVWEUVOUVPDCGUUCZUUDUUEYQUXEUYGUXGLVWFVWCVLUYFUYGUVPAVWLUUFUV
      GUXEUYEUVGUWMUXBUXEUXCUXDUXFUTVNUYFUYGUXAUXGUVOCLUYEUYGUXALUVGUVOUVPCCVDU
      VGDCUVOUXIVWGUUGWRUVGUXTUYEUVGUXBUYAUXTUVGUWMUXBUXCUXDUSUYBUYCUTVNUUHVIUU
      IUUJVJVVTVWDHUYGUVOUVPUUKUVSUYGUCZVVQUYJVVSVWCUVSUYGUVHVMVWMVVRVWBAUVSUYG
      UVPYRUULUUIUUMUUNHUVPAUVHVWLUUOUUPVTUUQAUVHUURXEVVOUVNUWJUWHUWKUVKUVMUVHU
      CVVOUVNUWJVLUVLCDUUSUVMUVLUVHCUVIUUTUVAUWGGUVLCUVBUVCUSUUAUVGUWMUVIUVDLUV
      JUWIVLUXCAUVHQUVEGHIUVDUVOBUVIDUVLFUVLWNUYMUVFUTUUA $.
      $( [17-May-2010] $) $( [13-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z M $.
    $( The first element of a group action ordered pair is a group.
       (Contributed by Jeff Hankins, 11-Aug-2009.) $)
    gagrp $p |- ( ( M e. A /\ <. G , M >. e. GrpAct ) -> G e. Grp ) $=
      ( vx vy vz wcel cop cga wa cgr crn cxp wf cgi cfv cv co wceq wral w3a
      eqid isga2 biimpa 3simp1d ) CAGZBCHIGZJBKGZBLZCLZMUJCNZBOPZDQZCRUMSEQZFQZ
      BRUMCRUNUOUMCRCRSFUITEUITJDUJTZUFUGUHUKUPUADEFAULBCUIUJUIUBUJUBULUBUCUDUE
      $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z G $.  $d x y z M $.  $d x y z X $.  $d x y z Y $.
    gaf.1 $e |- X = ran G $.
    gaf.2 $e |- Y = ran M $.
    $( The mapping of the group action operation.  (Contributed by Jeff
       Hankins, 11-Aug-2009.) $)
    gaf $p |- ( ( M e. A /\ <. G , M >. e. GrpAct )
              -> M : ( X X. Y ) --> Y ) $=
      ( vx vy vz wcel cop cga wa cgr cxp wf cgi cfv cv co wceq wral w3a eqid
      isga2 biimpa 3simp2d ) CAKZBCLMKZNBOKZDEPECQZBRSZHTZCUAUNUBITZJTZBUAUNCUA
      UOUPUNCUACUAUBJDUCIDUCNHEUCZUIUJUKULUQUDHIJAUMBCDEFGUMUEUFUGUH $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z G $.  $d x y z M $.  $d x y z U $.
    $d x y z Y $.
    gagrpid.1 $e |- Y = ran M $.
    gagrpid.2 $e |- U = ( Id ` G ) $.
    $( The identity of the group does not alter the base set.  (Contributed by
       Jeff Hankins, 11-Aug-2009.) $)
    gagrpid $p |- ( ( M e. B /\ <. G , M >. e. GrpAct /\ A e. Y ) -> ( U M A )
    = A ) $=
      ( vx vy vz wcel cop cga co wceq wa cv wi opreq2 id eqeq12d imbi2d crn
      wral cgr cxp wf w3a eqid isga2 biimpa 3simp3d ra4 pm3.26 imim2i 3syl
      com12 vtoclga 3impia ) EBLZDEMNLZAFLZCAEOZAPZVCVAVBQZVEVFCIRZEOZVGPZSVFVE
      SIAFVGAPZVIVEVFVJVHVDVGAVGACETVJUAUBUCVFVGFLZVIVFVIJRZKRZDOVGEOVLVMVGEOEO
      PKDUDZUEJVNUEZQZIFUEZVKVPSVKVISVFDUFLZVNFUGFEUHZVQVAVBVRVSVQUIIJKBCDEVNFV
      NUJGHUKULUMVPIFUNVPVIVKVIVOUOUPUQURUSURUT $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z G $.  $d x y z K $.
    $d x y z M $.  $d x y z X $.  $d x y z Y $.
    gaass.1 $e |- X = ran G $.
    gaass.2 $e |- Y = ran M $.
    $( An "associative" property for group actions.  (Contributed by Jeff
       Hankins, 11-Aug-2009.) $)
    gaass $p |- ( ( M e. K /\ <. G , M >. e. GrpAct /\ ( A e. X /\ B e. X /\ C
    e. Y ) ) -> ( ( A G B ) M C ) = ( A M ( B M C ) ) ) $=
      ( vy vz vx wcel cop cga w3a co wceq wa cv wi opreq1 opreq1d eqeq12d
      imbi2d opreq2 opreq2d wral cgi cfv cgr cxp wf eqid isga2 biimpa 3simp3d
      ra4 syl pm3.27 syl6 ra42 com13 3impia vtocl3ga com12 ) FENZDFOPNZAGNBGNCH
      NQZABDRZCFRZABCFRZFRZSZVJVHVITZVOVPKUAZLUAZDRZMUAZFRZVQVRVTFRZFRZSZUBZVPA
      VRDRZVTFRZAWBFRZSZUBVPVKVTFRZABVTFRZFRZSZUBVPVOUBKLMABCGGHVQASZWDWIVPWNWA
      WGWCWHWNVSWFVTFVQAVRDUCUDVQAWBFUCUEUFVRBSZWIWMVPWOWGWJWHWLWOWFVKVTFVRBADU
      GUDWOWBWKAFVRBVTFUCUHUEUFVTCSZWMVOVPWPWJVLWLVNVTCVKFUGWPWKVMAFVTCBFUGUHUE
      UFVQGNZVRGNZVTHNZWEVPWSWQWRTZWDVPWSWDLGUIKGUIZWTWDUBVPWSDUJUKZVTFRVTSZXAT
      ZXAVPXDMHUIZWSXDUBVPDULNZGHUMHFUNZXEVHVIXFXGXEQMKLEXBDFGHIJXBUOUPUQURXDMH
      USUTXCXAVAVBWDKLGGVCVBVDVEVFVGVE $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    gacan.1 $e |- X = ran G $.
    gacan.2 $e |- Y = ran M $.
    gacan.3 $e |- N = ( inv ` G ) $.
    $( Group inverses cancel in a group action.  (Contributed by Jeff Hankins,
       11-Aug-2009.) $)
    gacan $p |- ( ( M e. K /\ <. G , M >. e. GrpAct /\ ( A e. X /\ B e. Y /\ C
    e. Y ) ) -> ( ( A M B ) = C <-> ( ( N ` A ) M C ) = B ) ) $=
      ( wcel cop cga w3a co wceq cfv wa gaass 3simpl1 3simpl2 cgr grpinvcl
      gagrp sylan 3impa 3simp1 syl3an3 adantr 3ad2ant3 3simp2 3jca syl3anc cgi
      eqid grplinv syl2an opreq1d gagrpid eqtrd opreq2 adantl 3eqtr3rd 3simp3
      syld3an3 grprinv impbida ) FEMZDFNOMZAHMZBIMZCIMZPZPZABFQZCRZAGSZCFQZBRZV
      PVRTZVSADQZBFQZVSVQFQZBVTVJVKVSHMZVLVMPWDWERWBVSABDEFHIJKUAVJVKVOVRUBVJVK
      VOVRUCWBWFVLVMVPWFVRVJVKVLWFVOVJVKVLWFDUDMZVLWFVJVKTZADGHJLUEZEDFUFZUGUHV
      LVMVNUIZUJUKVPVLVRVOVJVLVKWKULZUKVPVMVRVOVJVMVKVLVMVNUMZULUKUNUOVPWDBRVRV
      PWDDUPSZBFQZBVPWCWNBFVJVKVOWCWNRZWGVLWPWHVOAWNDGHJWNUQZLURWJWKUSUHUTVJVKV
      MWOBRVOBEWNDFIKWQVAWMUJVBUKVRWEVTRVPVQCVSFVCVDVEVPWATAVSDQZCFQZAVTFQZCVQV
      PWSWTRZWAVJVKVLWFVNPXAVOAVSCDEFHIJKUAVPVLWFVNWLVJVKVOWFWGVLWFWHVOWIWJWKUS
      UHVOVJVNVKVLVMVNVFZULUNVGUKVPWSCRWAVPWSWNCFQZCVPWRWNCFVJVKVOWRWNRZWGVLXDW
      HVOAWNDGHJWQLVHWJWKUSUHUTVJVKVNXCCRVOCEWNDFIKWQVAXBUJVBUKWAWTVQRVPVTBAFVC
      VDVEVI $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d u v A $.  $d u v B $.  $d u v G $.  $d u v M $.  $d u v N $.
    $d u v X $.  $d u v Y $.  $d t u v $.  $d u v x $.  $d u v z $.
    gapmlem.1 $e |- X = ran G $.
    gapmlem.2 $e |- Y = ran M $.
    gapmlem.3 $e |- N = ( inv ` G ) $.
    $( Lemma for ~ gapm . $)
    gapmlem $p |- ( ( ( ( M e. B /\ <. G , M >. e. GrpAct /\ A e. X ) /\ t e. Y
    ) /\ ( z M t /\ z ( 2nd |` ( { A } X. _V ) ) x ) ) -> ( ( N ` A ) M t ) = x
    ) $=
      ( vu vv wcel cop cga w3a cv wa wbr c2nd csn cvv cxp cres cfv co wceq wex
      elxp biimpi ad2antll wi pm3.26 visset jctr impbii elsn bitri opeq1
      eqeq2d breq1 eleq1d anbi12d cdm wf gaf fdm syl 3adant3 adantr eleq2d
      anbi2d anbi1d simprll wb gacan 3simp1 ad2antrr 3simp2 3simp3 simprlr
      simplr 3jca syl3anc mpbid simprr eqtrd ex opelxp pm3.27bi anim2i sylani
      sylbid wfun ffun funbrfv df-opr eqeq1i syl6ibr opex breldm a1i jcad
      op2ndg mpan2 3ad2ant3 eqeq1d wfo fo2nd fofun funopfv ax-mp syl5bi
      syl2and com12 syl6bi adantrr sylbi impcom 19.23aivv com13 mpdi df-br
      opelres anbi2i syl5ib imp ) GEPZFGQRPZDIPZSZCTZJPZUAZBTZYOGUBZYRATZUCDUDZ
      UEUFZUGZUBZUAZDHUHYOGUIZYTUJZYQYSYRYTQZUCPZYRUUBPZUAZUAZUUGUUEYQUULYRNTZO
      TZQZUJZUUMUUAPZUUNUEPZUAZUAZOUKNUKZUUGUUJUVAYSUUIUUJUVANOYRUUAUEULUMUNUVA
      UULYQUUGUUTUULYQUUGUOZUOZNOUUSUUPUVCUUSUUMDUJZUUPUVCUOUUSUUQUVDUUSUUQUUQU
      URUPUUQUUROUQZURUSNDUTVAUVDUUPYRDUUNQZUJZUVCUVDUUOUVFYRUUMDUUNVBVCUULUVGU
      VBYSUUIUVGUVBUOUUJUVGYSUUIUAZUVBUVGUVHUVFYOGUBZUVFYTQZUCPZUAZUVBUVGYSUVIU
      UIUVKYRUVFYOGVDUVGUUHUVJUCYRUVFYTVBVEVFYQUVLUUGYQDUUNGUIZYOUJZUVFGVGZPZUA
      ZUUNYTUJZUUGUVIUVKYQUVQUVRUAUVNUVFIJUFZPZUAZUVRUAUUGYQUVQUWAUVRYQUVPUVTUV
      NYQUVOUVSUVFYNUVOUVSUJZYPYKYLUWBYMYKYLUAZUVSJGVHZUWBEFGIJKLVIZUVSJGVJVKVL
      VMVNVOVPYQUVNUUNJPZUAZUVRUUGUWAYQUWGUVRUAZUUGYQUWHUAZUUFUUNYTUWIUVNUUFUUN
      UJZYQUVNUWFUVRVQYKYLYMUWFYPSUVNUWJVRUWIDUUNYOFEGHIJKLMVSYNYKYPUWHYKYLYMVT
      WAYNYLYPUWHYKYLYMWBWAUWIYMUWFYPYNYMYPUWHYKYLYMWCWAYQUVNUWFUVRWDYNYPUWHWEW
      FWGWHYQUWGUVRWIWJWKUVTUWFUVNUVTYMUWFDUUNIJUVEWLWMWNWOWPYQUVIUVNUVPYQUVIUV
      FGUHZYOUJZUVNYQGWQZUVIUWLUOYNUWMYPYKYLUWMYMUWCUWDUWMUWEUVSJGWRVKVLVMUVFYO
      GCUQWSVKUVMUWKYODUUNGWTXAXBUVIUVPUOYQUVFYOGDUUNXCXDXEXFYQUVFUCUHZYTUJZUVR
      UVKYQUWNUUNYTYNUWNUUNUJZYPYMYKUWPYLYMUURUWPUVEDUUNIUEXGXHXIVMXJUEUEUCXKZU
      VKUWOUOZXLUWQUCWQUWRUEUEUCXMUVFYTUCAUQZXNVKXOXPXQXRXSXRXTXRXSYAYBYCYDYEUU
      DUUKYSUUDUUHUUCPUUKYRYTUUCYFYRYTUCUUBUWSYGVAYHYIYJ $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d t w x y z A $.  $d t w x y z B $.  $d t w x y z G $.  $d t w x y z M $.
    $d t w x y z X $.  $d t w x y z Y $.
    gapm.1 $e |- X = ran G $.
    gapm.2 $e |- Y = ran M $.
    gapm.3 $e |- F = ( M o. `' ( 2nd |` ( { A } X. _V ) ) ) $.
    $( The action of a particular group element is a permutation of the base
       set.  (Contributed by Jeff Hankins, 11-Aug-2009.) $)
    gapm $p |- ( ( M e. B /\ <. G , M >. e. GrpAct /\ A e. X ) -> F : Y
    -1-1-onto-> Y ) $=
      ( vt vx vy vz vw wf1o wfn ccnv wcel cop cga w3a dff1o4 wf cxp curry1f
      gaf 3adant3 3simp3 sylanc ffn syl c2nd csn cvv cres ccom wfun cdm wceq
      wa cima wrel frel dfrel2 sylib imaeq1d wss crn imassrn sseqtr4i a1i cv
      wex cgn cfv co opex eleq1 opeq1 eleq1d anbi12d cla4ev opelxpi snidg
      3ad2ant3 adantr oprex sylancl eqid wb gacan 3simpl1 3simpl2 3simpl3
      foprrn cgr grpinvcl gagrp sylan 3impa pm3.27 syl3anc 3jca mpbiri df-opr
      syl5eqr visset fnopfvb mpbid ex elima3 syl6ibr ssrdv eqssd eqtrd cin
      dmres ssv wfo fo2nd fofn fndm ax-mp df-ss mpbi eqtri imaeq2i syl5eq dmco
      wbr wmo wral relco a1i12 raleq wsbc wi wal gapmlem eqtr eqcoms syl9 syld
      expcom com4t 19.23adv 19.23aiv imp com13 brco brcnv anbi1i exbii bitr2i
      anbi12i imbi1i syl6ib breq2 sbcie anbi2i syl7ib 19.21adv ax-17 mo3
      r19.21aiv syl5cbir jcad dffun7 jcai ancomd df-fn sylibr cnveqi cnvco
      cocnvcnv1 3eqtri fneq1i sylanbrc ) GGCPCGQZCRZGQZEBSZDETUASZAFSZUBZGGCUCU
      WFGGCUDZUVTFGUEZGEUDZUWEUWGUWFFGAGECJUFUWCUWDUWIUWEBDEFGHIUGZUHZUWCUWDUWE
      UIUJGGCUKULUWFUMAUNZUOUEZUPZERZUQZGQZUWBUWFUWPURZUWPUSZGUTZVAUWQUWFUWTUWR
      UWFUWTUWRUWFUWORZUWNUSZVBZGUWSUWFUXAUWMVBZGUXCUWFUXDEUWMVBZGUWFUXAEUWMUWF
      EVCZUXAEUTUWFUWIUXFUWKUWHGEVDULEVEVFVGUWFUXEGUXEGVHUWFUXEEVIGEUWMVJIVKVLU
      WFKGUXEUWFKVMZGSZLVMZUWMSZUXIUXGTZESZVAZLVNZUXGUXESUWFUXHUXNAADVOVPZVPZUX
      GEVQZTZUWMSZUXRUXGTZESZUXNUWFUXHVAZUXMUXSUYAVALUXRAUXQVRUXIUXRUTZUXJUXSUX
      LUYAUXIUXRUWMVSUYCUXKUXTEUXIUXRUXGVTWAWBWCAUWLSZUXQUOSUXSUYBAUXQUWLUOWDUW
      FUYDUXHUWEUWCUYDUWDAFWEWFWGUXPUXGEWHWIUYBUXREVPZUXGUTZUYAUYBAUXQEVQZUXGUY
      EUYBUYGUXGUTZUXQUXQUTZUXQWJUWCUWDUWEUXQGSZUXHUBUYHUYIWKUYBAUXQUXGDBEUXOFG
      HIUXOWJZWLUWCUWDUWEUXHWMUWCUWDUWEUXHWNUYBUWEUYJUXHUWCUWDUWEUXHWOZUWIUXPFS
      ZUXHUYJUYBUXPUXGGFGEWPUWFUWIUXHUWKWGUWFUYMUXHUWCUWDUWEUYMDWQSUWEUYMUWCUWD
      VAZADUXOFHUYKWRBDEWSWTXAWGUWFUXHXBZXCZUYOXDXCXEAUXQEXFXGEUWHQZUXRUWHSZUYF
      UYAWKUYBUWHUXRUXGEKXHZXIUWFUYQUXHUWCUWDUYQUWEUYNUWIUYQUWJUWHGEUKULUHWGUWE
      UYJUYRUYBAUXQFGWDUYLUYPUJUJXJUJXKLUXGEUWMUYSXLXMXNXOXPUXBUWMUXAUXBUWMUMUS
      ZXQZUWMUMUWMXRUWMUYTVHVUAUWMUTUWMUOUYTUWMXSUOUOUMXTZUYTUOUTZYAVUBUMUOQVUC
      UOUOUMYBUOUMYCULYDVKUWMUYTYEYFYGYHYIUWNUWOYJYIUWFUWTUWPVCZUXGUXIUWPYKZLYL
      ZKUWSYMZVAUWRUWFUWTVUDVUGUWFUWTVUDUWNUWOYNYOUWTVUGVUFKGYMUWFVUFKUWSGYPUWF
      VUFKGUWFUXHVUEVUELMVMZYQZVAZUXIVUHUTZYRZMYSZLYSVUFUWFUXHVUMLUWFUXHVULMUWF
      UXHVUEUXGVUHUWPYKZVAZVUKVUJUWFUXHNVMZUXGEYKZVUPUXIUWNYKZVAZNVNZOVMZUXGEYK
      ZVVAVUHUWNYKZVAZOVNZVAZVUKYRVUOVUKYRVVFUXHUWFVUKVUTVVEUXHUWFVUKYRYRZVUSVV
      EVVGYRNVUSVVDVVGOUXHUWFVUSVVDVUKUWFUXHVUSVVDVUKYRZYRUYBVUSUXQUXIUTZVVHUYB
      VUSVVILNKABDEUXOFGHIUYKYTXKUYBVVDUXQVUHUTZVVIVUKUYBVVDVVJMOKABDEUXOFGHIUY
      KYTXKVVJVUKYRUXIUXQUXIUXQUTVVJVUKUXIUXQVUHUUAXKUUBUUCUUDUUEUUFUUGUUHUUIUU
      JVVFVUOVUKVUTVUEVVEVUNVUEUXGVUPUWOYKZVURVAZNVNVUTNUXGUXIUWNUWOUYSLXHUUKVV
      LVUSNVVKVUQVURUXGVUPEUYSNXHUULUUMUUNUUOVUNUXGVVAUWOYKZVVCVAZOVNVVEOUXGVUH
      UWNUWOUYSMXHZUUKVVNVVDOVVMVVBVVCUXGVVAEUYSOXHUULUUMUUNUUOUUPUUQUURVUIVUNV
      UEVUEVUNLVUHVVOUXIVUHUXGUWPUUSUUTUVAUVBUVCUVCVUELMVUEMUVDUVEXMUVFUVGUVHKL
      UWPUVIXMUVJUVKUWPGUVLUVMGUWAUWPUWAEUWNRZUQZRVVPRUWOUQUWPCVVQJUVNEVVPUVOUW
      NUWOUVPUVQUVRUVMUVS $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}


