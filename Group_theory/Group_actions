$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Group actions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c GrpAct $.
  $( Extend class definition to include the class of group actions. $)
  cga $a class GrpAct $.

  ${
    $d g m s x y z $.
    $( Define the class of all group actions. $)
    df-ga $a |- GrpAct = { <. g , m >. | ( g e. GrpOp /\ E. s ( m : ( ran g X.
  s ) --> s /\ A. x e. s ( ( ( Id ` g ) m x ) = x /\ A. y e. ran g A. z e. ran
    g ( ( y g z ) m x ) = ( y m ( z m x ) ) ) ) ) } $.
  $}

  ${
    $d g m s x y z G $.  $d g m s x y z M $.  $d g m U $.  $d g m y z X $.
    isgalem.1 $e |- X = ran G $.
    isgalem.2 $e |- U = ( Id ` G ) $.
    $( Lemma for ~ isga . $)
    isgalem $p |- ( M e. A -> ( <. G , M >. e. GrpAct <-> ( G e. GrpOp /\ E. s
  ( M : ( X X. s ) --> s /\ A. x e. s ( ( U M x ) = x /\ A. y e. X A. z e. X (
    ( y G z ) M x ) = ( y M ( z M x ) ) ) ) ) ) ) $=
      ( vg vm wcel cop cga cgr cv cxp wf co wceq wral wa wex cvv wbr df-br
      wrel crn cgi cfv copab relopab df-ga releqi mpbir brrelexi sylbir anim1i
      ancoms elisset adantr eleq2i eleq1 wb rneq syl6eqr xpeq1 feq2 3syl fveq2
      opreq1d eqeq1d opreq raleqbidv anbi12d ralbidv exbidv feq1 opreq2d eqtrd
      eqeq12d 2ralbidv anbi2d opelopabg syl5bb pm5.21nd ) GDNZFGOZPNZFQNZHIRZSZ
      WMGTZEARZGUAZWPUBZBRZCRZFUAZWPGUAZWSWTWPGUAZGUAZUBZCHUCBHUCZUDZAWMUCZUDZI
      UEZUDZFUFNZWIUDZWKWIXMWKXLWIWKFGPUGXLFGPUHFGPPUILRZQNZXNUJZWMSZWMMRZTZXNU
      KULZWPXRUAZWPUBZWSWTXNUAZWPXRUAZWSWTWPXRUAZXRUAZUBZCXPUCZBXPUCZUDZAWMUCZU
      DZIUEZUDZLMUMZUIYNLMUNPYOABCLMIUOZUPUQURUSUTVAXKWIXMXKXLWIWLXLXJFQVBVCUTV
      AWKWJYONXMXKPYOWJYPVDYNWLWNWMXRTZEWPXRUAZWPUBZXAWPXRUAZYFUBZCHUCZBHUCZUDZ
      AWMUCZUDZIUEZUDXKLMFGUFDXNFUBZXOWLYMUUGXNFQVEUUHYLUUFIUUHXSYQYKUUEUUHXPHU
      BXQWNUBXSYQVFUUHXPFUJHXNFVGJVHZXPHWMVIXQWNWMXRVJVKUUHYJUUDAWMUUHYBYSYIUUC
      UUHYAYRWPUUHXTEWPXRUUHXTFUKULEXNFUKVLKVHVMVNUUHYHUUBBXPHUUIUUHYGUUACXPHUU
      IUUHYDYTYFUUHYCXAWPXRWSWTXNFVOVMVNVPVPVQVRVQVSVQXRGUBZUUGXJWLUUJUUFXIIUUJ
      YQWOUUEXHWNWMXRGVTUUJUUDXGAWMUUJYSWRUUCXFUUJYRWQWPEWPXRGVOVNUUJUUAXEBCHHU
      UJYTXBYFXDXAWPXRGVOUUJYFWSYEGUAXDWSYEXRGVOUUJYEXCWSGWTWPXRGVOWAWBWCWDVQVR
      VQVSWEWFWGWH $.
      $( [17-May-2010] $) $( [10-Aug-2009] $)
  $}

  ${
    $d s A $.  $d s x y z G $.  $d s x y z M $.  $d s U $.  $d s y z X $.
    $d s x Y $.
    isga.1 $e |- X = ran G $.
    isga.2 $e |- Y = ran dom M $.
    isga.3 $e |- U = ( Id ` G ) $.
    $( The predicate "is a (left) group action."  The group ` G ` is said to
       act on the base set ` Y ` of the action, which is not assumed to have
       any special properties.  There is a related notion of right group
       action, but as the Wikipedia article explains, it is not mathematically
       interesting.  The way actions are usually thought of is that each
       element ` g ` of ` G ` is a permutation of the elements of ` Y ` (see
       ~ gapm ).  Since group theory was classically about symmetry groups, it
       is therefore likely that the notion of group action was useful even in
       early group theory.  (Contributed by Jeff Hankins, 10-Aug-2009.) $)
    isga $p |- ( M e. A -> ( <. G , M >. e. GrpAct
       <-> ( G e. GrpOp /\ M : ( X X. Y ) --> Y
   /\ A. x e. Y ( ( U M x ) = x /\ A. y e. X A. z e. X ( ( y G z ) M
    x ) = ( y M ( z M x ) ) ) ) ) ) $=
      ( vs wcel cop cga cgr cv cxp wf co wceq wral wa wex w3a isgalem wi cdm
      crn fdm adantr 3ad2ant1 rneqd c0 wne grpon0 3ad2ant2 rnxp syl eqtr2d
      syl6eqr wb xpeq2 feq23 mpancom raleq 3anbi23d biimpcd 3expb ancoms
      3adant3 mpd 3exp 19.23aiv impcom com12 cvv dmexg rnexg syl5eqel anbi12d
      cla4egv 3syl anim2d 3impb impbid bitrd ) GDNZFGOPNFQNZHMRZSZWKGTZEARZGUAW
      NUBBRZCRZFUAWNGUAWOWPWNGUAGUAUBCHUCBHUCUDZAWKUCZUDZMUEZUDZWJHISZIGTZWQAIU
      CZUFZABCDEFGHMJLUGWIXAXEXAWIXEWTWJWIXEUHZWSWJXFUHMWSWJWIXEWSWJWIUFZWKIUBZ
      XEXGWKGUIZUJZIXGXJWLUJZWKXGXIWLWSWJXIWLUBZWIWMXLWRWLWKGUKULUMUNXGHUOUPZXK
      WKUBWJWSXMWIFHJUQURHWKUSUTVAKVBWSWJXHXEUHZWIWJWSXNWJWMWRXNXHWJWMWRUFXEXHW
      MXCWRXDWJWLXBUBXHWMXCVCWKIHVDWLWKXBIGVEVFZWQAWKIVGZVHVIVJVKVLVMVNVOVPVQXE
      WIXAWJXCXDWIXAUHWIWJXCXDUDZUDXAWIXQWTWJWIXIVRNZIVRNXQWTUHGDVSXRIXJVRKXIVR
      VTWAWSXQMIVRXHWMXCWRXDXOXPWBWCWDWEVQWFVQWGWH $.
      $( [17-May-2010] $) $( [10-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z M $.  $d x y z X $.  $d x Y $.
    gafo.1 $e |- X = ran G $.
    gafo.2 $e |- Y = ran dom M $.
    $( A group action is onto its base set.  (Contributed by Jeff Hankins,
       10-Aug-2009.) $)
    gafo $p |- ( ( M e. A /\ <. G , M >. e. GrpAct ) -> M : ( X X. Y ) -onto->
    Y ) $=
      ( vx vy vz wcel cop cga wa cxp wfn crn wceq wfo cgr wf cgi cfv cv co
      wral w3a eqid isga biimpa ffn 3ad2ant2 wss frn wi wal ax-17 hbra1 hb3an
      ra4 simpl2l adantl grpoidcl 3ad2ant3 adantr simpl1 fnoprvrn syl111anc
      eqeltrrd 3exp1 sylcom com4t 3imp 19.21ai dfss2 sylibr eqssd jca syl
      df-fo ) CAKZBCLMKZNZCDEOZPZCQZERZNZWDECSWCBTKZWDECUAZBUBUCZHUDZCUEZWLRZIU
      DZJUDZBUEWLCUEWOWPWLCUECUERJDUFIDUFZNZHEUFZUGZWHWAWBWTHIJAWKBCDEFGWKUHZUI
      UJWTWEWGWJWIWEWSWDECUKZULWTWFEWJWIWFEUMWSWDECUNULWTWLEKZWLWFKZUOZHUPEWFUM
      WTXEHWIWJWSHWIHUQWJHUQWRHEURUSWIWJWSXEWSXCWIWJXDWSXCWRWIWJXDUOUOWRHEUTXCW
      RWIWJXDXCWRWIUGZWJNZWMWLWFWNWQXCWIWJVAXGWEWKDKZXCWMWFKWJWEXFXBVBXFXHWJWIX
      CXHWRWKBDFXAVCVDVEXCWRWIWJVFDEWKWLCVGVHVIVJVKVLVMVNHEWFVOVPVQVRVSWDECVTVP
      $.
      $( [17-May-2010] $) $( [10-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z M $.  $d y z X $.  $d x Y $.
    isga2.1 $e |- X = ran G $.
    isga2.2 $e |- Y = ran M $.
    isga2.3 $e |- U = ( Id ` G ) $.
    $( An equivalent form of ~ isga .  (Contributed by Jeff Hankins,
       10-Aug-2009.) $)
    isga2 $p |- ( M e. A -> ( <. G , M >. e. GrpAct <-> ( G e. GrpOp /\ M : ( X
    X. Y ) --> Y /\ A. x e. Y ( ( U M x ) = x /\ A. y e. X A. z e. X ( ( y G z
    ) M x ) = ( y M ( z M x ) ) ) ) ) ) $=
      ( wcel cop cga cgr cxp wf cv co wceq wral wa w3a cdm crn simpl wfo eqid
      gafo forn syl5eq syl jca fdm rneqd adantl c0 wne grpon0 rnxp adantr
      eqtr2d 3adant3 anim2i isga wb xpeq2 feq23 mpancom raleq 3anbi23d eqcoms
      sylan9bb pm5.21nd ) GDMZFGNOMZFPMZHIQZIGRZEASZGTWAUABSZCSZFTWAGTWBWCWAGTG
      TUACHUBBHUBUCZAIUBZUDZVPIGUEZUFZUAZUCVPVQUCZVPWIVPVQUGWJHWHQZWHGUHZWIDFGH
      WHJWHUIZUJWLIGUFWHKWKWHGUKULUMUNWFWIVPVRVTWIWEVRVTUCWHVSUFZIVTWHWNUAVRVTW
      GVSVSIGUOUPUQVRWNIUAZVTVRHURUSWOFHJUTHIVAUMVBVCVDVEVPVQVRWKWHGRZWDAWHUBZU
      DZWIWFABCDEFGHWHJWMLVFWRWFVGWHIWHIUAZWPVTWQWEVRWKVSUAWSWPVTVGWHIHVHWKWHVS
      IGVIVJWDAWHIVKVLVMVNVO $.
      $( [17-May-2010] $) $( [10-Aug-2009] $)
  $}

  ${
    $d x y z G $.
    $( The action of a group on the empty set.  (Contributed by Jeff Hankins,
       11-Aug-2009.) $)
    ga0 $p |- ( G e. GrpOp -> <. G , (/) >. e. GrpAct ) $=
      ( vx vy vz cgr wcel crn c0 cxp wf cgi cfv cv co wceq wral wa w3a cop cga
      id f00 eqid xp0 mpbir2an a1i ral0 3jca cvv wb 0ex rn0 eqcomi isga2 ax-mp
      sylibr ) AEFZUQAGZHIZHHJZAKLZBMZHNVBOCMZDMZANVBHNVCVDVBHNHNODURPCURPQZBHP
      ZRZAHSTFZUQUQUTVFUQUAUTUQUTHHOUSHOUSHUBHUCURUDUEUFVFUQVEBUGUFUHHUIFVHVGUJ
      UKBCDUIVAAHURHURUCHGHULUMVAUCUNUOUP $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z G $.  $d x y z S $.  $d x y z X $.
    gaid.1 $e |- X = ran G $.
    $( The trivial action of a group on any set.  Each group element
       corresponds to the identity permutation.  (Contributed by Jeff Hankins,
       11-Aug-2009.) $)
    gaid $p |- ( ( G e. GrpOp /\ S e. A ) -> <. G , ( 2nd |` ( X X. S ) ) >. e.
    GrpAct ) $=
      ( vx vy vz cgr wcel wa c2nd cxp cres cop cga crn wf cgi cfv cv co wceq
      wral w3a simpl f2ndres a1i df-opr eqid grpoidcl 3ad2ant1 simp3 opelxpi
      syl11anc fvres syl syl5eq fvex visset op2nd syl6eq wi grpocl 3exp 3imp
      syl6req syl6eqr oprex 3eqtr4g simp2 syl5eqel eqeltrd syl5req 3eqtrd
      r19.21adv r19.21aiv jca 3expia 3jca wb c0 wne wfo grpon0 fo2ndres forn
      3syl adantr xpeq2 feq23 mpancom raleq 3anbi23d mpbird cvv rnexg xpexg
      sylan fex sylancr isga2 ) CIJZBAJZKZCLDBMZNZOPJZXCDXGQZMZXIXGRZCSTZFUAZXG
      UBZXMUCZGUAZHUAZCUBZXMXGUBZXPXQXMXGUBZXGUBZUCZHDUDZGDUDZKZFXIUDZUEZXEYGXC
      XFBXGRZYEFBUDZUEZXEXCYHYIXCXDUFYHXEDBUGZUHXEYEFBXCXDXMBJZYEXCXDYLUEZXOYDY
      MXNXLXMOZLTZXMYMXNYNXGTZYOXLXMXGUIYMYNXFJZYPYOUCYMXLDJZYLYQXCXDYRYLXLCDEX
      LUJZUKULXCXDYLUMZXLXMDBUNUOYNXFLUPUQURXLXMCSUSFUTZVAVBYMYCGDYMXPDJZYBHDYM
      UUBXQDJZYBYMUUBUUCUEZXSXRXMOZLTZXPXTOZLTZYAUUDXSUUEXGTZUUFXRXMXGUIUUDUUEX
      FJZUUIUUFUCUUDXRDJZYLUUJYMUUBUUCUUKXCXDUUBUUCUUKVCVCYLXCUUBUUCUUKXPXQCDEV
      DVEULVFYMUUBYLUUCYTULZXRXMDBUNUOUUEXFLUPUQURUUDXMXTUUFUUHUUDXMXQXMOZXGTZX
      TUUDUUNUUMLTZXMUUDUUMXFJZUUNUUOUCUUDUUCYLUUPYMUUBUUCUMUULXQXMDBUNUOUUMXFL
      UPUQZXQXMHUTUUAVAZVGXQXMXGUIZVHXRXMXPXQCVIUUAVAXPXTGUTXQXMXGVIVAVJUUDYAUU
      GXGTZUUHXPXTXGUIUUDUUGXFJZUUTUUHUCUUDUUBXTBJUVAYMUUBUUCVKUUDXTUUOBUUDXTUU
      NUUOUUSUUQURUUDUUOXMBUURUULVLVMXPXTDBUNUOUUGXFLUPUQVNVOVEVPVQVRVSVQVTXEXI
      BUCZYGYJWAXCUVBXDXCDWBWCXFBXGWDUVBCDEWEDBWFXFBXGWGWHWIUVBXKYHYFYIXCXJXFUC
      UVBXKYHWAXIBDWJXJXIXFBXGWKWLYEFXIBWMWNUQWOXEXGWPJZXHYGWAXEYHXFWPJZUVCYKXC
      DWPJXDUVDXCDCQWPECIWQVLDBWPAWRWSXFBWPXGWTXAFGHWPXLCXGDXIEXIUJYSXBUQWO $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z H $.  $d x y z X $.  $d x y z Y $.
    ssga.1 $e |- X = ran G $.
    ssga.2 $e |- Y = ran H $.
    $( A subgroup acts on its parent group.  (Contributed by Jeff Hankins,
       13-Aug-2009.) $)
    ssga $p |- ( H e. ( SubGrpOp ` G )
        -> <. H , ( G |` ( Y X. X ) ) >. e. GrpAct ) $=
      ( vx vy vz csubg cfv wcel cxp cres cop cga cgr crn wf cgi cv co wceq
      wral wa w3a wss issubg simp2bi wfn wfun cdm wfo simp1bi grpfo fofun
      funres syl 3syl subgrnss wex elxp wi ssel impcom adantlr simplr opelxpi
      syl11anc ex adantl wb eleq1 adantr sylibrd 19.23aivv com12 syl5bi ssrdv
      fof fdm sseqtr4d ssdmres sylib df-fn sylanbrc resss rnss ax-mp sseqtr4i
      jctir df-f sylibr df-opr eqid grpoidcl 3ad2ant2 sylbi sylan fvres subgid
      opreq1d grpolid eqtrd syl5eqr syl5eq ad2antrr simprl simprr foprrn
      syl111anc simpll simpr subgopr sylc ancoms adantll 3eqtr4g adantrl
      opreq2d ad2ant2l ssel2 ad2ant2rl sylanl1 syl5eqelr eqeltrd ad2ant2r 3jca
      grpoass 3eqtr4rd 3eqtrd r19.21aivv jca r19.21aiva cima df-ima imassrn
      a1i opeq1 fveq2d eqeq1d mpbird jca31 visset opelxp bicomi sseldd
      eleqtrrd funopfvb anbi12d mpbid opex eleq1d cla4ev syl6 elima3 syl6ibr
      eqssd xpeq2 feq23 mpancom raleq 3anbi23d cvv resexg isga2 ) BAJKLZBADCMZN
      ZOPLZBQLZDUVIRZMZUVLUVISZBTKZGUAZUVIUBZUVPUCZHUAZIUAZBUBZUVPUVIUBZUVSUVTU
      VPUVIUBZUVIUBZUCZIDUDHDUDZUEZGUVLUDZUFZUVGUWIUVKUVHCUVISZUWGGCUDZUFZUVGUV
      KUWJUWKUVGAQLZUVKBAUGZABUHZUIZUVGUVIUVHUJZUVLCUGZUEUWJUVGUWQUWRUVGUVIUKZU
      VIULUVHUCZUWQUVGUWMCCMZCAUMZUWSUVGUWMUVKUWNUWOUNZACEUOZUXBAUKZUWSUXACAUPZ
      UVHAUQURUSUVGUVHAULZUGUWTUVGUVHUXAUXGUVGDCUGZUVHUXAUGABDCEFUTZUXHGUVHUXAU
      VPUVHLUVPUVSUVTOZUCZUVSDLZUVTCLZUEZUEZIVAHVAZUXHUVPUXALZHIUVPDCVBUXPUXHUX
      QUXOUXHUXQVCHIUXOUXHUXJUXALZUXQUXNUXHUXRVCUXKUXNUXHUXRUXNUXHUEUVSCLZUXMUX
      RUXLUXHUXSUXMUXHUXLUXSDCUVSVDVEVFUXLUXMUXHVGUVSUVTCCVHVIVJVKUXKUXQUXRVLUX
      NUVPUXJUXAVMVNVOVPVQVRVSURUVGUWMUXBUXGUXAUCZUXCUXDUXBUXACASZUXTUXACAVTZUX
      ACAWAZURUSWBUVHAWCWDUVIUVHWEWFUVLARZCUVIAUGUVLUYDUGAUVHWGUVIAWHWIEWJWKUVH
      CUVIWLWMUVGUWGGCUVGUVPCLZUEZUVRUWFUYFUVQUVOUVPOZUVIKZUVPUVOUVPUVIWNUYFUYH
      UYGAKZUVPUYFUYGUVHLZUYHUYIUCUVGUVODLZUYEUYJUVGUWMUVKUWNUFZUYKUWOUVKUWMUYK
      UWNUVOBDFUVOWOZWPZWQWRUVOUVPDCVHWSUYGUVHAWTURUYFUYIUVOUVPAUBZUVPUVOUVPAWN
      UYFUYOATKZUVPAUBZUVPUYFUVOUYPUVPAUVGUVOUYPUCZUYEUVOUYPABUYPWOZUYMXAZVNXBU
      VGUWMUYEUYQUVPUCUXCUVPUYPACEUYSXCZWSXDXEXDXFUYFUWEHIDDUYFUXLUVTDLZUEZUWEU
      YFVUCUEZUWBUWAUVPOZAKZUVSUVTAUBZUVPAUBZUWDVUDUWBVUEUVIKZVUFUWAUVPUVIWNVUD
      VUEUVHLZVUIVUFUCVUDUWADLZUYEVUJVUDDDMZDBSZUXLVUBVUKUVGVUMUYEVUCUVGUYLVUMU
      WOUVKUWMVUMUWNUVKVULDBUMVUMBDFUOVULDBVTURWQWRXGUYFUXLVUBXHZUYFUXLVUBXIUVS
      UVTDDDBXJXKUVGUYEVUCVGZUWAUVPDCVHVIVUEUVHAWTURXFVUDVUFUWAUVPAUBVUHUWAUVPA
      WNVUDUWAVUGUVPAVUDUVGVUCUWAVUGUCUVGUYEVUCXLUYFVUCXMUVSUVTABDFXNXOXBXEVUDU
      VSUWCAUBZUVSUVTUVPAUBZAUBZUWDVUHVUDUWCVUQUVSAUYFVUBUWCVUQUCUXLUYFVUBUEZUV
      TUVPOZUVIKZVUTAKZUWCVUQVUSVUTUVHLZVVAVVBUCZUYEVUBVVCUVGVUBUYEVVCUVTUVPDCV
      HXPZXQVUTUVHAWTZURUVTUVPUVIWNZUVTUVPAWNZXRXSXTVUDUVSUWCOZUVIKZVVIAKZUWDVU
      PVUDVVIUVHLZVVJVVKUCVUDUXLUWCCLVVLVUNVUDUWCVVBCVUDUWCVVAVVBVVGVUDVVCVVDUY
      EVUBVVCUVGUXLVVEYAVVFURXFVUDVVBVUQCVVHVUDUYAUXMUYEVUQCLUVGUYAUYEVUCUVGUWM
      UXBUYAUXCUXDUYBUSXGUVGUXHUYEVUCUXMUXIUXHVUBUXMUYEUXLDCUVTYBYCZYDVUOUVTUVP
      CCCAXJXKYEYFUVSUWCDCVHVIVVIUVHAWTURUVSUWCUVIWNUVSUWCAWNXRVUDUWMUXSUXMUYEU
      FZVUHVURUCUVGUWMUYEVUCUXCXGUVGUXHUYEVUCVVNUXIUXHUYEUEVUCUEUXSUXMUYEUXHUXL
      UXSUYEVUBDCUVSYBYGVVMUXHUYEVUCVGYHYDUVSUVTUVPACEYIVIYJYKVJYLYMYNYHUVGUVLC
      UCZUWIUWLVLUVGUVLAUVHYOZCAUVHYPUVGVVPCVVPCUGUVGVVPUYDCAUVHYQEWJYRUVGGCVVP
      UVGUYEUVSUVHLZUVSUVPOZALZUEZHVAZUVPVVPLUVGUYEUYJUYGUVPOZALZUEZVWAUVGUYEVW
      DUYFUYKUYEUEZUYIUVPUCZUEVWDUYFUYKUYEVWFUVGUYKUYEUVGUVKUYKUWPUYNURZVNUVGUY
      EXMUYFVWFUYPUVPOZAKZUVPUCZUVGUWMUYEVWJUXCUWMUYEUEVWIUYQUVPUYPUVPAWNVUAXEW
      SUVGVWFVWJVLZUYEUVGUYRVWKUYTUYRUYIVWIUVPUYRUYGVWHAUVOUYPUVPYSYTUUAURVNUUB
      UUCUYFVWEUYJVWFVWCVWEUYJVLUYFUYJVWEUVOUVPDCGUUDZUUEUUFYRUYFUXEUYGUXGLVWFV
      WCVLUVGUXEUYEUVGUWMUXBUXEUXCUXDUXFUSVNUYFUYGUXAUXGUVGUVOCLUYEUYGUXALUVGDC
      UVOUXIVWGUUGUVOUVPCCVHWSUVGUXTUYEUVGUXBUYAUXTUVGUWMUXBUXCUXDURUYBUYCUSVNU
      UHUYGUVPAVWLUUIVIUUJUUKVJVVTVWDHUYGUVOUVPUULUVSUYGUCZVVQUYJVVSVWCUVSUYGUV
      HVMVWMVVRVWBAUVSUYGUVPYSUUMUUJUUNUUOHUVPAUVHVWLUUPUUQVSUURXEVVOUVNUWJUWHU
      WKUVKUVMUVHUCVVOUVNUWJVLUVLCDUUSUVMUVLUVHCUVIUUTUVAUWGGUVLCUVBUVCURUUBUVG
      UWMUVIUVDLUVJUWIVLUXCAUVHQUVEGHIUVDUVOBUVIDUVLFUVLWOUYMUVFUSUUB $.
      $( [17-May-2010] $) $( [13-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z M $.
    $( The first element of a group action ordered pair is a group.
       (Contributed by Jeff Hankins, 11-Aug-2009.) $)
    gagrp $p |- ( ( M e. A /\ <. G , M >. e. GrpAct ) -> G e. GrpOp ) $=
      ( vx vy vz wcel cop cga wa cgr crn cxp wf cgi cfv cv co wceq wral w3a
      eqid isga2 biimpa simp1d ) CAGZBCHIGZJBKGZBLZCLZMUJCNZBOPZDQZCRUMSEQZFQZB
      RUMCRUNUOUMCRCRSFUITEUITJDUJTZUFUGUHUKUPUADEFAULBCUIUJUIUBUJUBULUBUCUDUE
      $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z G $.  $d x y z M $.  $d x y z X $.  $d x y z Y $.
    gaf.1 $e |- X = ran G $.
    gaf.2 $e |- Y = ran M $.
    $( The mapping of the group action operation.  (Contributed by Jeff
       Hankins, 11-Aug-2009.) $)
    gaf $p |- ( ( M e. A /\ <. G , M >. e. GrpAct )
              -> M : ( X X. Y ) --> Y ) $=
      ( vx vy vz wcel cop cga wa cgr cxp wf cgi cfv cv co wceq wral w3a eqid
      isga2 biimpa simp2d ) CAKZBCLMKZNBOKZDEPECQZBRSZHTZCUAUNUBITZJTZBUAUNCUAU
      OUPUNCUACUAUBJDUCIDUCNHEUCZUIUJUKULUQUDHIJAUMBCDEFGUMUEUFUGUH $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z G $.  $d x y z M $.  $d x y z U $.
    $d x y z Y $.
    gagrpid.1 $e |- Y = ran M $.
    gagrpid.2 $e |- U = ( Id ` G ) $.
    $( The identity of the group does not alter the base set.  (Contributed by
       Jeff Hankins, 11-Aug-2009.) $)
    gagrpid $p |- ( ( M e. B /\ <. G , M >. e. GrpAct /\ A e. Y ) -> ( U M A )
    = A ) $=
      ( vx vy vz wcel cop cga co wceq wa cv wi opreq2 id eqeq12d imbi2d crn
      wral cgr cxp wf w3a eqid isga2 biimpa simp3d ra4 simpl imim2i 3syl com12
      vtoclga 3impia ) EBLZDEMNLZAFLZCAEOZAPZVCVAVBQZVEVFCIRZEOZVGPZSVFVESIAFVG
      APZVIVEVFVJVHVDVGAVGACETVJUAUBUCVFVGFLZVIVFVIJRZKRZDOVGEOVLVMVGEOEOPKDUDZ
      UEJVNUEZQZIFUEZVKVPSVKVISVFDUFLZVNFUGFEUHZVQVAVBVRVSVQUIIJKBCDEVNFVNUJGHU
      KULUMVPIFUNVPVIVKVIVOUOUPUQURUSURUT $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z G $.  $d x y z K $.
    $d x y z M $.  $d x y z X $.  $d x y z Y $.
    gaass.1 $e |- X = ran G $.
    gaass.2 $e |- Y = ran M $.
    $( An "associative" property for group actions.  (Contributed by Jeff
       Hankins, 11-Aug-2009.) $)
    gaass $p |- ( ( M e. K /\ <. G , M >. e. GrpAct /\ ( A e. X /\ B e. X /\ C
    e. Y ) ) -> ( ( A G B ) M C ) = ( A M ( B M C ) ) ) $=
      ( vy vz vx wcel cop cga w3a co wceq wa cv wi opreq1 opreq1d eqeq12d
      imbi2d opreq2 opreq2d wral cgi cfv cgr cxp wf eqid isga2 biimpa simp3d
      ra4 syl simpr syl6 ra42 com13 3impia vtocl3ga com12 ) FENZDFOPNZAGNBGNCHN
      QZABDRZCFRZABCFRZFRZSZVJVHVITZVOVPKUAZLUAZDRZMUAZFRZVQVRVTFRZFRZSZUBZVPAV
      RDRZVTFRZAWBFRZSZUBVPVKVTFRZABVTFRZFRZSZUBVPVOUBKLMABCGGHVQASZWDWIVPWNWAW
      GWCWHWNVSWFVTFVQAVRDUCUDVQAWBFUCUEUFVRBSZWIWMVPWOWGWJWHWLWOWFVKVTFVRBADUG
      UDWOWBWKAFVRBVTFUCUHUEUFVTCSZWMVOVPWPWJVLWLVNVTCVKFUGWPWKVMAFVTCBFUGUHUEU
      FVQGNZVRGNZVTHNZWEVPWSWQWRTZWDVPWSWDLGUIKGUIZWTWDUBVPWSDUJUKZVTFRVTSZXATZ
      XAVPXDMHUIZWSXDUBVPDULNZGHUMHFUNZXEVHVIXFXGXEQMKLEXBDFGHIJXBUOUPUQURXDMHU
      SUTXCXAVAVBWDKLGGVCVBVDVEVFVGVE $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    gacan.1 $e |- X = ran G $.
    gacan.2 $e |- Y = ran M $.
    gacan.3 $e |- N = ( inv ` G ) $.
    $( Group inverses cancel in a group action.  (Contributed by Jeff Hankins,
       11-Aug-2009.) $)
    gacan $p |- ( ( M e. K /\ <. G , M >. e. GrpAct /\ ( A e. X /\ B e. Y /\ C
    e. Y ) ) -> ( ( A M B ) = C <-> ( ( N ` A ) M C ) = B ) ) $=
      ( wcel cop cga w3a co wceq cfv wa simpl1 simpl2 simp1 cgr gagrp
      grpoinvcl sylan 3impa syl3an3 adantr simpl31 simpl32 gaass syl113anc cgi
      eqid grpolinv syl2an opreq1d simp2 gagrpid eqtrd opreq2 adantl 3eqtr3rd
      3ad2ant3 simp3 3jca syld3an3 grporinv impbida ) FEMZDFNOMZAHMZBIMZCIMZPZP
      ZABFQZCRZAGSZCFQZBRZVRVTTZWAADQZBFQZWAVSFQZBWBWDVLVMWAHMZVNVOWFWGRVLVMVQV
      TUAVLVMVQVTUBVRWHVTVQVLVMVNWHVNVOVPUCZVLVMVNWHVLVMTZDUDMZVNWHEDFUEZADGHJL
      UFZUGUHUIUJVNVOVPVLVMVTUKVNVOVPVLVMVTULWAABDEFHIJKUMUNVRWFBRVTVRWFDUOSZBF
      QZBVRWEWNBFVLVMVQWEWNRZWJWKVNWPVQWLWIAWNDGHJWNUPZLUQURUHUSVQVLVMVOWOBRVNV
      OVPUTBEWNDFIKWQVAUIVBUJVTWGWBRVRVSCWAFVCVDVEVRWCTAWADQZCFQZAWBFQZCVSVRWSW
      TRZWCVLVMVQVNWHVPPXAVRVNWHVPVQVLVNVMWIVFVLVMVQWHWJWKVNWHVQWLWIWMURUHVQVLV
      PVMVNVOVPVGZVFVHAWACDEFHIJKUMVIUJVRWSCRWCVRWSWNCFQZCVRWRWNCFVLVMVQWRWNRZW
      JWKVNXDVQWLWIAWNDGHJWQLVJURUHUSVQVLVMVPXCCRXBCEWNDFIKWQVAUIVBUJWCWTVSRVRW
      BBAFVCVDVEVK $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d u v A $.  $d u v B $.  $d u v G $.  $d u v M $.  $d u v N $.
    $d u v X $.  $d u v Y $.  $d t u v $.  $d u v x $.  $d u v z $.
    gapmlem.1 $e |- X = ran G $.
    gapmlem.2 $e |- Y = ran M $.
    gapmlem.3 $e |- N = ( inv ` G ) $.
    $( Lemma for ~ gapm . $)
    gapmlem $p |- ( ( ( ( M e. B /\ <. G , M >. e. GrpAct /\ A e. X ) /\ t e. Y
    ) /\ ( z M t /\ z ( 2nd |` ( { A } X. _V ) ) x ) ) -> ( ( N ` A ) M t ) = x
    ) $=
      ( vu vv wcel cop cga w3a cv wa wbr c2nd csn cvv cxp cres cfv co wceq
      df-br visset opelres bitri anbi2i wex elxp biimpi ad2antll wi simpl jctr
      impbii elsn opeq1 eqeq2d breq1 eleq1d anbi12d cdm wfun wf gaf ffun syl
      3adant3 adantr funbrfv df-opr eqeq1i syl6ibr opex breldm a1i jcad wfo
      fo2nd fofun funopfv ax-mp op2ndg mpan2 3ad2ant3 eqeq1d syl5ib fdm eleq2d
      anbi2d anbi1d opelxp simprbi anim2i simprll wb simpll1 simpll2 simpll3
      simprlr simplr gacan syl113anc mpbid simprr eqtrd ex sylani sylbid
      syl2and com12 syl6bi adantrr sylbi impcom 19.23aivv com13 mpdi syl5bi
      imp ) GEPZFGQRPZDIPZSZCTZJPZUAZBTZYMGUBZYPATZUCDUDZUEUFZUGZUBZUAZDHUHYMGU
      IZYRUJZUUCYQYPYRQZUCPZYPYTPZUAZUAZYOUUEUUBUUIYQUUBUUFUUAPUUIYPYRUUAUKYPYR
      UCYTAULZUMUNUOYOUUJYPNTZOTZQZUJZUULYSPZUUMUEPZUAZUAZOUPNUPZUUEUUHUUTYQUUG
      UUHUUTNOYPYSUEUQURUSUUTUUJYOUUEUUSUUJYOUUEUTZUTZNOUURUUOUVBUURUULDUJZUUOU
      VBUTUURUUPUVCUURUUPUUPUUQVAUUPUUQOULZVBVCNDVDUNUVCUUOYPDUUMQZUJZUVBUVCUUN
      UVEYPUULDUUMVEVFUUJUVFUVAYQUUGUVFUVAUTUUHUVFYQUUGUAZUVAUVFUVGUVEYMGUBZUVE
      YRQZUCPZUAZUVAUVFYQUVHUUGUVJYPUVEYMGVGUVFUUFUVIUCYPUVEYRVEVHVIYOUVKUUEYOU
      VHDUUMGUIZYMUJZUVEGVJZPZUAZUVJUUMYRUJZUUEYOUVHUVMUVOYOUVHUVEGUHZYMUJZUVMY
      OGVKZUVHUVSUTYLUVTYNYIYJUVTYKYIYJUAZIJUFZJGVLZUVTEFGIJKLVMZUWBJGVNVOVPVQU
      VEYMGCULVRVOUVLUVRYMDUUMGVSVTWAUVHUVOUTYOUVEYMGDUUMWBWCWDWEUVJUVEUCUHZYRU
      JZYOUVQUEUEUCWFZUVJUWFUTZWGUWGUCVKUWHUEUEUCWHUVEYRUCUUKWIVOWJYOUWEUUMYRYL
      UWEUUMUJZYNYKYIUWIYJYKUUQUWIUVDDUUMIUEWKWLWMVQWNWOYOUVPUVQUAUVMUVEUWBPZUA
      ZUVQUAUUEYOUVPUWKUVQYOUVOUWJUVMYOUVNUWBUVEYLUVNUWBUJZYNYIYJUWLYKUWAUWCUWL
      UWDUWBJGWPVOVPVQWQWRWSUWKYOUVMUUMJPZUAZUVQUUEUWJUWMUVMUWJYKUWMDUUMIJUVDWT
      XAXBYOUWNUVQUAZUUEYOUWOUAZUUDUUMYRUWPUVMUUDUUMUJZYOUVMUWMUVQXCUWPYIYJYKUW
      MYNUVMUWQXDYIYJYKYNUWOXEYIYJYKYNUWOXFYIYJYKYNUWOXGYOUVMUWMUVQXHYLYNUWOXID
      UUMYMFEGHIJKLMXJXKXLYOUWNUVQXMXNXOXPXQXRXSXTXSYAXSXTYBYCYDYEYFYGYH $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}

  ${
    $d t w x y z A $.  $d t w x y z B $.  $d t w x y z G $.  $d t w x y z M $.
    $d t w x y z X $.  $d t w x y z Y $.
    gapm.1 $e |- X = ran G $.
    gapm.2 $e |- Y = ran M $.
    gapm.3 $e |- F = ( M o. `' ( 2nd |` ( { A } X. _V ) ) ) $.
    $( The action of a particular group element is a permutation of the base
       set.  (Contributed by Jeff Hankins, 11-Aug-2009.) $)
    gapm $p |- ( ( M e. B /\ <. G , M >. e. GrpAct /\ A e. X ) -> F : Y
    -1-1-onto-> Y ) $=
      ( vt vx vy vz vw wcel cop cga w3a wfn ccnv wf1o wf cxp gaf 3adant3 simp3
      curry1f syl11anc ffn syl c2nd csn cvv cres ccom wfun cdm wceq wa cima
      dmco cin dmres wss ssv wfo fo2nd fofn fndm ax-mp sseqtr4i df-ss mpbi
      eqtri imaeq2i wrel frel dfrel2 sylib imaeq1d crn imassrn a1i cv wex cgn
      cfv co snidg 3ad2ant3 adantr oprex opelxpi sylancl df-opr eqid wb simpl1
      simpl2 simpl3 cgr gagrp grpoinvcl sylan 3impa simpr foprrn syl111anc
      gacan syl113anc mpbiri syl5eqr visset fnopfvb mpbid opex eleq1 opeq1
      eleq1d anbi12d cla4ev ex elima3 syl6ibr ssrdv eqssd eqtrd syl5eq wbr wmo
      wral relco a1i12 wsbc wi wal breq2 sbcie anbi2i gapmlem eqtr eqcoms syl9
      syld expcom com4t 19.23adv 19.23aiv imp com13 brco brcnv anbi1i exbii
      bitr2i anbi12i imbi1i syl6ib syl7bi 19.21adv ax-17 mo3 r19.21aiv raleq
      syl5ibrcom jcad dffun7 jcai ancomd df-fn sylibr cnveqi cnvco cocnvcnv1
      3eqtri fneq1i dff1o4 sylanbrc ) EBPZDEQRPZAFPZSZCGTZCUAZGTZGGCUBUWCGGCUCZ
      UWDUWCFGUDZGEUCZUWBUWGUVTUWAUWIUWBBDEFGHIUEZUFZUVTUWAUWBUGFGAGECJUHUIGGCU
      JUKUWCULAUMZUNUDZUOZEUAZUPZGTZUWFUWCUWPUQZUWPURZGUSZUTUWQUWCUWTUWRUWCUWTU
      WRUWCUWSUWOUAZUWNURZVAZGUWNUWOVBUWCUXCUXAUWMVAZGUXBUWMUXAUXBUWMULURZVCZUW
      MULUWMVDUWMUXEVEUXFUWMUSUWMUNUXEUWMVFUNUNULVGZUXEUNUSZVHUXGULUNTUXHUNUNUL
      VIUNULVJUKVKVLUWMUXEVMVNVOVPUWCUXDEUWMVAZGUWCUXAEUWMUWCEVQZUXAEUSUWCUWIUX
      JUWKUWHGEVRUKEVSVTWAUWCUXIGUXIGVEUWCUXIEWBGEUWMWCIVLWDUWCKGUXIUWCKWEZGPZL
      WEZUWMPZUXMUXKQZEPZUTZLWFZUXKUXIPUWCUXLUXRUWCUXLUTZAADWGWHZWHZUXKEWIZQZUW
      MPZUYCUXKQZEPZUXRUXSAUWLPZUYBUNPUYDUWCUYGUXLUWBUVTUYGUWAAFWJWKWLUYAUXKEWM
      AUYBUWLUNWNWOUXSUYCEWHZUXKUSZUYFUXSUYHAUYBEWIZUXKAUYBEWPUXSUYJUXKUSZUYBUY
      BUSZUYBWQUXSUVTUWAUWBUYBGPZUXLUYKUYLWRUVTUWAUWBUXLWSUVTUWAUWBUXLWTUVTUWAU
      WBUXLXAZUXSUWIUYAFPZUXLUYMUWCUWIUXLUWKWLUWCUYOUXLUVTUWAUWBUYOUVTUWAUTZDXB
      PUWBUYOBDEXCADUXTFHUXTWQZXDXEXFWLUWCUXLXGZUYAUXKGFGEXHXIZUYRAUYBUXKDBEUXT
      FGHIUYQXJXKXLXMUXSEUWHTZUYCUWHPZUYIUYFWRUWCUYTUXLUVTUWAUYTUWBUYPUWIUYTUWJ
      UWHGEUJUKUFWLUXSUWBUYMVUAUYNUYSAUYBFGWNUIUWHUYCUXKEKXNZXOUIXPUXQUYDUYFUTL
      UYCAUYBXQUXMUYCUSZUXNUYDUXPUYFUXMUYCUWMXRVUCUXOUYEEUXMUYCUXKXSXTYAYBUIYCL
      UXKEUWMVUBYDYEYFYGYHYIYIUWCUWTUWPVQZUXKUXMUWPYJZLYKZKUWSYLZUTUWRUWCUWTVUD
      VUGUWCUWTVUDUWNUWOYMYNUWCVUGUWTVUFKGYLUWCVUFKGUWCUXLVUEVUELMWEZYOZUTZUXMV
      UHUSZYPZMYQZLYQVUFUWCUXLVUMLUWCUXLVULMVUJVUEUXKVUHUWPYJZUTZUWCUXLVUKVUIVU
      NVUEVUEVUNLVUHMXNZUXMVUHUXKUWPYRYSYTUWCUXLNWEZUXKEYJZVUQUXMUWNYJZUTZNWFZO
      WEZUXKEYJZVVBVUHUWNYJZUTZOWFZUTZVUKYPVUOVUKYPVVGUXLUWCVUKVVAVVFUXLUWCVUKY
      PYPZVUTVVFVVHYPNVUTVVEVVHOUXLUWCVUTVVEVUKUWCUXLVUTVVEVUKYPZYPUXSVUTUYBUXM
      USZVVIUXSVUTVVJLNKABDEUXTFGHIUYQUUAYCUXSVVEUYBVUHUSZVVJVUKUXSVVEVVKMOKABD
      EUXTFGHIUYQUUAYCVVKVUKYPUXMUYBUXMUYBUSVVKVUKUXMUYBVUHUUBYCUUCUUDUUEUUFUUG
      UUHUUIUUJUUKVVGVUOVUKVVAVUEVVFVUNVUEUXKVUQUWOYJZVUSUTZNWFVVANUXKUXMUWNUWO
      VUBLXNUULVVMVUTNVVLVURVUSUXKVUQEVUBNXNUUMUUNUUOUUPVUNUXKVVBUWOYJZVVDUTZOW
      FVVFOUXKVUHUWNUWOVUBVUPUULVVOVVEOVVNVVCVVDUXKVVBEVUBOXNUUMUUNUUOUUPUUQUUR
      UUSUUTUVAUVAVUELMVUEMUVBUVCYEUVDVUFKUWSGUVEUVFUVGKLUWPUVHYEUVIUVJUWPGUVKU
      VLGUWEUWPUWEEUWNUAZUPZUAVVPUAUWOUPUWPCVVQJUVMEVVPUVNUWNUWOUVOUVPUVQUVLGGC
      UVRUVS $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)
  $}


