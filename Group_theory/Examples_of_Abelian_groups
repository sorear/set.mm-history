$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Examples of Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d u x y z A $.
    ablsn.1 $e |- A e. _V $.
    $( The Abelian group operation for the singleton group. $)
    ablsn $p |- { <. <. A , A >. , A >. } e. AbelOp $=
      ( vx vy cop csn grpsn cdm dmsnop xpsn eqtr4i cv wcel wceq co elsn opreq12
      cxp wa opreq2 opreq1 sylan9eq eqtr4d syl2anb isabloi ) CDAAEZAEFZAFZABGUG
      HUFFUHUHRUFAIAABBJKCLZUHMUIANZDLZANZUIUKUGOZUKUIUGOZNUKUHMCAPDAPUJULSUMAA
      UGOZUNUIAUKAUGQUJULUNUKAUGOUOUIAUKUGTUKAAUGUAUBUCUDUE $.
      $( [5-Nov-2006] $)

    $( The identity element of the trivial group.  (Contributed by FL,
       21-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    gidsn $p |- ( Id ` { <. <. A , A >. , A >. } ) = A $=
      ( cop csn cgr wcel cgi cfv wceq grpsn crn opex rnsnop eqid grpoidcl elsni
      eqcomi mp2b ) AACZACDZEFTGHZADZFUAAIABJUATUBTKUBSAAALBMQUANOUAAPR $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)

    $( The inverse function of the trivial group.  (Contributed by FL,
       21-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    ginvsn $p |- ( inv ` { <. <. A , A >. , A >. } ) = ( _I |` { A } ) $=
      ( cid cfv cop csn cres cgn cvv wcel wceq fvi ax-mp opeq2i sneqi wf1o f1of
      wfn wf mp2b f1ovi ffn fnressn mp2an cgr grpsn crn opex rnsnop eqcomi eqid
      grpinvf fsn mpbi 3eqtr4ri ) AACDZEZFZAAEZFZCAFZGZUSAEFZHDZUQUSUPAAAIJZUPA
      KBAILMNOCIRZVEVBURKIICPIICSVFUAIICQIICUBTBIACUCUDVAVAVDSZVDUTKVCUEJVAVAVD
      PVGABUFVCVDVAVCUGVAUSAAAUHBUIUJVDUKULVAVAVDQTAAVDBBUMUNUO $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    $d x y z A $.
    $( Complex number addition is an Abelian group operation. $)
    cnaddablo $p |- + e. AbelOp $=
      ( vx vy vz caddc cc cc0 cv cneg ax-cnex ax-addopr addass 0cn addid2 negcl
      wcel co wceq addcom mpdan negid eqtr3d isgrpoi cxp fdmi isabloi ) ABDEABC
      FDAGZHZEIJUFBGZCGKLUFMUFNZUFEOZUFUGDPZUGUFDPZFUJUGEOUKULQUIUFUGRSUFTUAUBE
      EUCEDJUDUFUHRUE $.
      $( [5-Nov-2006] $)

    $( The group identity element of complex number addition is zero.
       (Contributed by Steve Rodriguez, 3-Dec-2006.) $)
    cnid $p |- 0 = ( Id ` + ) $=
      ( vy vx vz caddc cgi cfv cv co wceq wral crio cc0 cgr wcel cabl cnaddablo
      cc ablogrpo ax-mp cxp ax-addopr fdmi grprn eqid grpoidval addid2 rgen 0cn
      wreu grpoideu ax-17 a17d opreq1 eqeq1d ralbidv riota2f mp2an mpbi eqtr2i
      wb ) DEFZAGZBGZDHZVCIZBQJZAQKZLDMNZVAVGIDONVHPDRSZBAVADQDQVIQQTQDUAUBUCZV
      AUDUESLVCDHZVCIZBQJZVGLIZVLBQVCUFUGLQNZVFAQUIZVMVNUTUHVHVPVIBADQVJUJSVFVM
      ACQLCGLNAUKVOVMAULVBLIZVEVLBQVQVDVKVCVBLVCDUMUNUOUPUQURUS $.
      $( [21-Dec-2013] $) $( [28-Nov-2006] $)

    $( Value of the group inverse of complex number addition.  (Contributed by
       Steve Rodriguez, 3-Dec-2006.) $)
    addinv $p |- ( A e. CC -> ( ( inv ` + ) ` A ) = -u A ) $=
      ( vy vx cc wcel caddc cgn cfv cc0 wceq crio cneg cgr mpan 0cn wreu eqeq1d
      cv co mpbid cabl cnaddablo ablogrpo ax-mp ax-addopr fdmi grprn grpoinvval
      cnid eqid cmin df-neg opreq1i npcan syl5eq wb negcl negeu mpan2 wa addcom
      cxp reubidva ax-17 a17d opreq1 riota2f syl11anc eqtrd ) ADEZAFGHZHZBRZAFS
      ZIJZBDKZALZFMEZVJVLVPJFUAEVRUBFUCUDZBAIFVKDFDVSDDVBDFUEUFUGUIVKUJUHNVJVQA
      FSZIJZVPVQJZVJVTIAUKSZAFSZIVQWCAFAULUMIDEZVJWDIJOIAUNNUOVJVQDEZVOBDPZWAWB
      UPAUQVJAVMFSZIJZBDPZWGVJWEWJOBAIURUSVJWIVOBDVJVMDEUTWHVNIAVMVAQVCTVOWABCD
      VQCRVQEBVDWFWABVEVMVQJVNVTIVMVQAFVFQVGVHTVI $.
      $( [21-Dec-2013] $) $( [3-Dec-2006] $)

    $( The real numbers under addition comprise a subgroup of the complex
       numbers under addition.  (Contributed by Paul Chapman, 25-Apr-2008.) $)
    readdsubg $p |- ( + |` ( RR X. RR ) ) e. ( SubGrpOp ` + ) $=
      ( vx vy cc0 caddc cr cxp cres cgn cfv cc cabl wcel cgr cnaddablo ablogrpo
      ax-mp ax-addopr fdmi eqid cv grprn cnid ax-resscn readdcl 0re cneg addinv
      wceq recn syl renegcl eqeltrd issubgi ) ABCDDEEFGZDHIZJEDKLDMLNDOPZDJUPJJ
      FJDQRUAUBUOSUCUNSATZBTUDUEUQELZUQUOIZUQUFZEURUQJLUSUTUHUQUIUQUGUJUQUKULUM
      $.
      $( [25-Apr-2008] $)

    $( The integers under addition comprise a subgroup of the complex numbers
       under addition.  (Contributed by Paul Chapman, 25-Apr-2008.) $)
    zaddsubg $p |- ( + |` ( ZZ X. ZZ ) ) e. ( SubGrpOp ` + ) $=
      ( vx vy cc0 caddc cz cxp cres cgn cfv cc cabl wcel cgr cnaddablo ablogrpo
      ax-mp ax-addopr fdmi eqid cv grprn cnid zsscn zaddcl cneg wceq zcn addinv
      0z syl znegcl eqeltrd issubgi ) ABCDDEEFGZDHIZJEDKLDMLNDOPZDJUPJJFJDQRUAU
      BUOSUCUNSATZBTUDUIUQELZUQUOIZUQUEZEURUQJLUSUTUFUQUGUQUHUJUQUKULUM $.
      $( [25-Apr-2008] $)

    $( Nonzero complex number multiplication is an Abelian group operation.
       (Contributed by Steve Rodriguez, 12-Feb-2007.) $)
    ablmul $p |- ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) e. AbelOp $=
      ( vx vy vz cmul cc cc0 c1 cv cvv wcel mulnzcnopr wceq oprvres wne eldifsn
      co wa jca eqtrd eldifi csn cdif cxp cres cdiv ax-cnex difexg w3a ad2ant2r
      ax-mp mulcl mulne0 syl2anb sylibr eqeltrd anim1i 3impa syl 3adant3 mulass
      opreq1d syl3an eqcomd 3adant1 opreq2d foprcl eqeltrrd sylan2 3impb 3eqtrd
      3eqtr3d ax-1cn ax-1ne0 mpbir2an mpan mulid2 recne0 3imtr4i mpancom recid2
      reccl sylbi isgrpoi fdmi mulcom syl2an ancoms 3eqtr4d isabloi ) ABDEFUAZU
      BZWKUCZUDZWKABCGWMGAHZUEPZWKEIJWKIJUFEWJIUGUJKWNWKJZBHZWKJZCHZWKJZUHZWNWQ
      WMPZWSWMPZWNWQDPZWSDPZWNWQWSDPZDPZWNWQWSWMPZWMPZXAXCXBWSDPZXEXAXBWKJZWTQZ
      XCXJLWPWRWTXLWPWRQZXKWTXMXBXDWKWNWQWKWKDMZXMXDEJZXDFNZQZXDWKJWPWNEJZWNFNZ
      QZWQEJZWQFNZQZXQWRWNEFOZWQEFOXTYCQXOXPXRYAXOXSYBWNWQUKUIWNWQULRUMXDEFOUNU
      OUPUQXBWSWKWKDMURXAXBXDWSDWPWRXBXDLWTXNUSVASWPXRWRYAWTWSEJXEXGLWNEWJTZWQE
      WJTZWSEWJTWNWQWSUTVBXAXGWNXHDPZXIXAXFXHWNDWRWTXFXHLWPWRWTQZXHXFWQWSWKWKDM
      ZVCVDZVEZXAXGWNXFWMPZYGXIWPWRWTXGYLLZYHWPXFWKJZYMYHXHXFWKYIWQWSWKWKWKWMKV
      FVGWPYNQYLXGWNXFWKWKDMVCVHVIYKXAXFXHWNWMYJVEVKSVJGWKJZGEJGFNGEFOVLVMVNZWP
      GWNWMPZGWNDPZWNYOWPYQYRLYPGWNWKWKDMVOWPXRYRWNLYEWNVPURSXTWOEJZWOFNZQWPWOW
      KJZXTYSYTWNWAWNVQRYDWOEFOVRZWPWOWNWMPZWOWNDPZGUUAWPUUCUUDLUUBWOWNWKWKDMVS
      WPXTUUDGLYDWNVTWBSWCWLWKWMKWDXMXDWQWNDPZXBWQWNWMPZWPXRYAXDUUELWRYEYFWNWQW
      EWFXNWRWPUUFUUELWQWNWKWKDMWGWHWI $.
      $( [7-Nov-2006] $)

    $( The group identity element of nonzero complex number multiplication is
       one.  (Contributed by Steve Rodriguez, 23-Feb-2007.)  (Revised by Mario
       Carneiro, 17-Dec-2013.) $)
    mulid $p |- ( Id ` ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) ) = 1 $=
      ( vy vx vz cmul cc cc0 csn cdif cxp cres cgi cv co wceq wral crio c1 wcel
      cfv ax-mp cgr cabl ablmul ablogrpo mulnzcnopr fdmi eqid grpoidval eldifsn
      grprn wne ax-1cn ax-1ne0 mpbir2an oprvres mpan eldifi syl eqtrd rgen wreu
      mulid2 grpoideu ax-17 a17d opreq1 eqeq1d ralbidv riota2f mp2an mpbi eqtri
      wb ) DEFGZHZVOIZJZKSZALZBLZVQMZVTNZBVOOZAVOPZQVQUARZVRWDNVQUBRWEUCVQUDTZB
      AVRVQVOVQVOWFVPVOVQUEUFUJZVRUGUHTQVTVQMZVTNZBVOOZWDQNZWIBVOVTVORZWHQVTDMZ
      VTQVORZWLWHWMNWNQERQFUKQEFUIULUMUNZQVTVOVODUOUPWLVTERWMVTNVTEVNUQVTVBURUS
      UTWNWCAVOVAZWJWKVMWOWEWPWFBAVQVOWGVCTWCWJACVOQCLQRAVDWNWJAVEVSQNZWBWIBVOW
      QWAWHVTVSQVTVQVFVGVHVIVJVKVL $.
      $( [17-Dec-2013] $) $( [3-Dec-2006] $)
  $}


