$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Examples of Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d u x y z A $.
    ablsn.1 $e |- A e. _V $.
    $( The Abelian group operation for the singleton group. $)
    ablsn $p |- { <. <. A , A >. , A >. } e. AbelOp $=
      ( vx vy cop csn grposn cdm cxp dmsnop xpsn eqtr4i cv wcel wceq co elsn wa
      opreq12 opreq2 opreq1 sylan9eq eqtr4d syl2anb isabloi ) CDAAEZAEFZAFZABGU
      GHUFFUHUHIUFAJAABBKLCMZUHNUIAOZDMZAOZUIUKUGPZUKUIUGPZOUKUHNCAQDAQUJULRUMA
      AUGPZUNUIAUKAUGSUJULUNUKAUGPUOUIAUKUGTUKAAUGUAUBUCUDUE $.
      $( [5-Nov-2006] $)

    $( The identity element of the trivial group.  (Contributed by FL,
       21-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    gidsn $p |- ( Id ` { <. <. A , A >. , A >. } ) = A $=
      ( cop csn cgr wcel cgi wceq grposn opex rnsnop eqcomi eqid grpoidcl elsni
      cfv crn mp2b ) AACZACDZEFTGPZADZFUAAHABIUATUBTQUBSAAAJBKLUAMNUAAOR $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)

    $( The inverse function of the trivial group.  (Contributed by FL,
       21-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    ginvsn $p |- ( inv ` { <. <. A , A >. , A >. } ) = ( _I |` { A } ) $=
      ( cid cfv cop csn cres cgn cvv wcel wceq fvi ax-mp opeq2i sneqi wf1o f1of
      wfn wf mp2b f1ovi ffn fnressn mp2an cgr grposn crn rnsnop eqcomi grpoinvf
      opex eqid fsn mpbi 3eqtr4ri ) AACDZEZFZAAEZFZCAFZGZUSAEFZHDZUQUSUPAAAIJZU
      PAKBAILMNOCIRZVEVBURKIICPIICSVFUAIICQIICUBTBIACUCUDVAVAVDSZVDUTKVCUEJVAVA
      VDPVGABUFVCVDVAVCUGVAUSAAAUKBUHUIVDULUJVAVAVDQTAAVDBBUMUNUO $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    $d x y z A $.
    $( Complex number addition is an Abelian group operation. $)
    cnaddablo $p |- + e. AbelOp $=
      ( vx vy vz caddc cc cc0 cv cneg cnex ax-addopr addass 0cn addid2 negcl co
      wcel wceq addcom mpdan negid eqtr3d isgrpoi cxp fdmi isabloi ) ABDEABCFDA
      GZHZEIJUFBGZCGKLUFMUFNZUFEPZUFUGDOZUGUFDOZFUJUGEPUKULQUIUFUGRSUFTUAUBEEUC
      EDJUDUFUHRUE $.
      $( [5-Nov-2006] $)

    $( The group identity element of complex number addition is zero.
       (Contributed by Steve Rodriguez, 3-Dec-2006.) $)
    cnid $p |- 0 = ( Id ` + ) $=
      ( vy vx vz caddc cgi cfv cv co wceq wral crio cc0 cgr wcel cabl cnaddablo
      cc ablogrpo ax-mp cxp ax-addopr fdmi grporn eqid grpoidval addid2 rgen wb
      wreu grpoideu ax-17 a17d opreq1 eqeq1d ralbidv riota2f mp2an mpbi eqtr2i
      0cn ) DEFZAGZBGZDHZVCIZBQJZAQKZLDMNZVAVGIDONVHPDRSZBAVADQDQVIQQTQDUAUBUCZ
      VAUDUESLVCDHZVCIZBQJZVGLIZVLBQVCUFUGLQNZVFAQUIZVMVNUHUTVHVPVIBADQVJUJSVFV
      MACQLCGLNAUKVOVMAULVBLIZVEVLBQVQVDVKVCVBLVCDUMUNUOUPUQURUS $.
      $( [21-Dec-2013] $) $( [28-Nov-2006] $)

    $( Value of the group inverse of complex number addition.  (Contributed by
       Steve Rodriguez, 3-Dec-2006.) $)
    addinv $p |- ( A e. CC -> ( ( inv ` + ) ` A ) = -u A ) $=
      ( vy vx cc wcel caddc cgn cfv cc0 wceq crio cneg cgr mpan 0cn wreu eqeq1d
      cv co mpbid cabl cnaddablo ablogrpo ax-mp ax-addopr fdmi grporn cnid eqid
      cxp grpoinvval df-neg opreq1i npcan syl5eq wb negcl negeu mpan2 wa addcom
      cmin reubidva ax-17 a17d opreq1 riota2f syl11anc eqtrd ) ADEZAFGHZHZBRZAF
      SZIJZBDKZALZFMEZVJVLVPJFUAEVRUBFUCUDZBAIFVKDFDVSDDUJDFUEUFUGUHVKUIUKNVJVQ
      AFSZIJZVPVQJZVJVTIAVBSZAFSZIVQWCAFAULUMIDEZVJWDIJOIAUNNUOVJVQDEZVOBDPZWAW
      BUPAUQVJAVMFSZIJZBDPZWGVJWEWJOBAIURUSVJWIVOBDVJVMDEUTWHVNIAVMVAQVCTVOWABC
      DVQCRVQEBVDWFWABVEVMVQJVNVTIVMVQAFVFQVGVHTVI $.
      $( [21-Dec-2013] $) $( [3-Dec-2006] $)

    $( The real numbers under addition comprise a subgroup of the complex
       numbers under addition.  (Contributed by Paul Chapman, 25-Apr-2008.) $)
    readdsubg $p |- ( + |` ( RR X. RR ) ) e. ( SubGrpOp ` + ) $=
      ( vx vy cc0 caddc cr cxp cres cgn cfv cc cabl wcel cgr cnaddablo ablogrpo
      ax-mp ax-addopr fdmi eqid cv grporn cnid ax-resscn readdcl cneg wceq recn
      0re addinv syl renegcl eqeltrd issubgi ) ABCDDEEFGZDHIZJEDKLDMLNDOPZDJUPJ
      JFJDQRUAUBUOSUCUNSATZBTUDUHUQELZUQUOIZUQUEZEURUQJLUSUTUFUQUGUQUIUJUQUKULU
      M $.
      $( [25-Apr-2008] $)

    $( The integers under addition comprise a subgroup of the complex numbers
       under addition.  (Contributed by Paul Chapman, 25-Apr-2008.) $)
    zaddsubg $p |- ( + |` ( ZZ X. ZZ ) ) e. ( SubGrpOp ` + ) $=
      ( vx vy cc0 caddc cz cxp cres cgn cfv cc cabl wcel cgr cnaddablo ablogrpo
      ax-mp ax-addopr fdmi eqid cv grporn cnid zsscn zaddcl 0z cneg wceq addinv
      zcn syl znegcl eqeltrd issubgi ) ABCDDEEFGZDHIZJEDKLDMLNDOPZDJUPJJFJDQRUA
      UBUOSUCUNSATZBTUDUEUQELZUQUOIZUQUFZEURUQJLUSUTUGUQUIUQUHUJUQUKULUM $.
      $( [25-Apr-2008] $)

    $( Nonzero complex number multiplication is an Abelian group operation.
       (Contributed by Steve Rodriguez, 12-Feb-2007.) $)
    ablmul $p |- ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) e. AbelOp $=
      ( vx vy vz cmul cc cc0 c1 cv cvv wcel mulnzcnopr wceq oprvres wne eldifsn
      co wa jca eqtrd eldifi csn cdif cxp cres cdiv cnex difexg ax-mp w3a mulcl
      ad2ant2r mulne0 syl2anb sylibr eqeltrd anim1i 3impa 3adant3 mulass syl3an
      opreq1d eqcomd 3adant1 opreq2d foprcl eqeltrrd sylan2 3impb 3eqtrd ax-1cn
      3eqtr3d ax-1ne0 mpbir2an mulid2 reccl recne0 3imtr4i mpancom recid2 sylbi
      syl mpan isgrpoi fdmi mulcom syl2an ancoms 3eqtr4d isabloi ) ABDEFUAZUBZW
      KUCZUDZWKABCGWMGAHZUEPZWKEIJWKIJUFEWJIUGUHKWNWKJZBHZWKJZCHZWKJZUIZWNWQWMP
      ZWSWMPZWNWQDPZWSDPZWNWQWSDPZDPZWNWQWSWMPZWMPZXAXCXBWSDPZXEXAXBWKJZWTQZXCX
      JLWPWRWTXLWPWRQZXKWTXMXBXDWKWNWQWKWKDMZXMXDEJZXDFNZQZXDWKJWPWNEJZWNFNZQZW
      QEJZWQFNZQZXQWRWNEFOZWQEFOXTYCQXOXPXRYAXOXSYBWNWQUJUKWNWQULRUMXDEFOUNUOUP
      UQXBWSWKWKDMWAXAXBXDWSDWPWRXBXDLWTXNURVASWPXRWRYAWTWSEJXEXGLWNEWJTZWQEWJT
      ZWSEWJTWNWQWSUSUTXAXGWNXHDPZXIXAXFXHWNDWRWTXFXHLWPWRWTQZXHXFWQWSWKWKDMZVB
      VCZVDZXAXGWNXFWMPZYGXIWPWRWTXGYLLZYHWPXFWKJZYMYHXHXFWKYIWQWSWKWKWKWMKVEVF
      WPYNQYLXGWNXFWKWKDMVBVGVHYKXAXFXHWNWMYJVDVKSVIGWKJZGEJGFNGEFOVJVLVMZWPGWN
      WMPZGWNDPZWNYOWPYQYRLYPGWNWKWKDMWBWPXRYRWNLYEWNVNWASXTWOEJZWOFNZQWPWOWKJZ
      XTYSYTWNVOWNVPRYDWOEFOVQZWPWOWNWMPZWOWNDPZGUUAWPUUCUUDLUUBWOWNWKWKDMVRWPX
      TUUDGLYDWNVSVTSWCWLWKWMKWDXMXDWQWNDPZXBWQWNWMPZWPXRYAXDUUELWRYEYFWNWQWEWF
      XNWRWPUUFUUELWQWNWKWKDMWGWHWI $.
      $( [7-Nov-2006] $)

    $( The group identity element of nonzero complex number multiplication is
       one.  (Contributed by Steve Rodriguez, 23-Feb-2007.)  (Revised by Mario
       Carneiro, 17-Dec-2013.) $)
    mulid $p |- ( Id ` ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) ) = 1 $=
      ( vy vx vz cmul cc cc0 csn cdif cxp cres cgi cv co wceq wral crio c1 wcel
      cfv ax-mp cgr cabl ablmul ablogrpo mulnzcnopr fdmi eqid grpoidval eldifsn
      grporn ax-1cn ax-1ne0 mpbir2an oprvres mpan eldifi mulid2 eqtrd rgen wreu
      wne wb grpoideu ax-17 a17d opreq1 eqeq1d ralbidv riota2f mp2an mpbi eqtri
      syl ) DEFGZHZVOIZJZKSZALZBLZVQMZVTNZBVOOZAVOPZQVQUARZVRWDNVQUBRWEUCVQUDTZ
      BAVRVQVOVQVOWFVPVOVQUEUFUJZVRUGUHTQVTVQMZVTNZBVOOZWDQNZWIBVOVTVORZWHQVTDM
      ZVTQVORZWLWHWMNWNQERQFVAQEFUIUKULUMZQVTVOVODUNUOWLVTERWMVTNVTEVNUPVTUQVMU
      RUSWNWCAVOUTZWJWKVBWOWEWPWFBAVQVOWGVCTWCWJACVOQCLQRAVDWNWJAVEVSQNZWBWIBVO
      WQWAWHVTVSQVTVQVFVGVHVIVJVKVL $.
      $( [17-Dec-2013] $) $( [3-Dec-2006] $)
  $}


