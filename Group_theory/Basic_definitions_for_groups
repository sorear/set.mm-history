$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Basic definitions for groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Grp $.
  $c Id $.
  $c inv $.

  $( Extend class notation with the class of all group operations. $)
  cgr $a class Grp $.

  $( Extend class notation with a function mapping a group operation to the
     group's identity element. $)
  cgi $a class Id $.

  $( Extend class notation with a function mapping a group operation to the
     inverse function for the group. $)
  cgn $a class inv $.

  ${
    $d f g t u x y z $.
    $( Define the class of all group operations.  The base set for a group
       can be determined from its group operation.  Based on the definition in
       Exercise 28 of [Herstein] p. 54. $)
    df-grp $a |- Grp = { g | E. t ( g : ( t X. t ) --> t
       /\ A. x e. t A. y e. t A. z e. t ( ( x g y ) g z ) = ( x g ( y g z ) )
   /\ E. u e. t A. x e. t ( ( u g x ) = x /\ E. y e. t ( y g x ) = u ) ) } $.

    $( Define a function that maps a group operation to the group's identity
       element. $)
    df-gid $a |- Id = { <. g , y >. | ( g e. Grp /\ y =
                 U. { u e. ran g | A. x e. ran g ( u g x ) = x } ) } $.

    $( Define a function that maps a group operation to the group's inverse
       function. $)
    df-ginv $a |- inv = { <. g , f >. | ( g e. Grp /\ f = { <. x , y >. | ( x
  e. ran g /\ y = U. { z e. ran g | ( z g x ) = ( Id ` g ) } ) } ) } $.
  $}

  ${
    $d g t u x y z G $.  $d g t u x y z X $.
    isgrp.1 $e |- X = ran G $.
    $( The predicate "is a group operation."  Note that ` X ` is the base set
       of the group. $)
    isgrp $p |- ( G e. A -> ( G e. Grp <-> ( G : ( X X. X ) --> X /\
       A. x e. X A. y e. X A. z e. X ( ( x G y ) G z ) = ( x G ( y G z ) )
   /\ E. u e. X A. x e. X ( ( u G x ) = x /\ E. y e. X ( y G x ) = u ) ) ) ) $=
      ( vt vg wcel cgr cv crn wceq cxp wf co wral wrex wa w3a wex feq1 opreq
      opreq1d eqtrd opreq2d eqeq12d ralbidv 2ralbidv eqeq1d rexbidv anbi12d
      rexralbidv 3anbi123d exbidv df-grp elab2g wfo opreq2 id eqcom syl6bb
      rcla4v eqeq2d rcla4ev ex syld pm3.26 r19.20si syl5 r19.22sdv impcom
      r19.21aiva anim2i fooprval sylibr forn eqcomd syl 3adant2 pm4.71ri exbii
      cvv wb rnexg eqeq2i xpeq1 xpeq2 feq2 feq3 bitrd raleq1 raleqd rexeq1
      anbi2d rexeqd sylbir ceqsexgv ) FEKZFLKZIMZFNZOZXCXCPZXCFQZAMZBMZFRZCMZFR
      ZXHXIXKFRZFRZOZCXCSZBXCSZAXCSZDMZXHFRZXHOZXIXHFRZXSOZBXCTZUAZAXCSZDXCTZUB
      ZUAZIUCZGGPZGFQZXOCGSZBGSZAGSZYAYCBGTZUAZAGSZDGTZUBZXAXBYHIUCZYJXFXCJMZQZ
      XHXIUUBRZXKUUBRZXHXIXKUUBRZUUBRZOZCXCSZBXCSAXCSZXSXHUUBRZXHOZXIXHUUBRZXSO
      ZBXCTZUAZAXCSDXCTZUBZIUCUUAJFLEUUBFOZUURYHIUUSUUCXGUUJXRUUQYGXFXCUUBFUDUU
      SUUIXPABXCXCUUSUUHXOCXCUUSUUEXLUUGXNUUSUUEUUDXKFRXLUUDXKUUBFUEUUSUUDXJXKF
      XHXIUUBFUEUFUGUUSUUGXHUUFFRXNXHUUFUUBFUEUUSUUFXMXHFXIXKUUBFUEUHUGUIUJUKUU
      SUUPYEDAXCXCUUSUULYAUUOYDUUSUUKXTXHXSXHUUBFUEULUUSUUNYCBXCUUSUUMYBXSXIXHU
      UBFUEULUMUNUOUPUQABCDIJURUSYHYIIYHXEXGYGXEXRXGYGUAZXFXCFUTZXEUUTXGXKXSXIF
      RZOZBXCTZDXCTZCXCSZUAUVAYGUVFXGYGUVECXCXKXCKZYGUVEUVGYFUVDDXCUVGYAAXCSZUV
      DYFUVGUVHXKXSXKFRZOZUVDYAUVJAXKXCXHXKOZYAUVIXKOUVJUVKXTUVIXHXKXHXKXSFVAUV
      KVBUIUVIXKVCVDVEUVGUVJUVDUVCUVJBXKXCXIXKOUVBUVIXKXIXKXSFVAVFVGVHVIYEYAAXC
      YAYDVJVKVLVMVNVOVPDBCXCXCXCFVQVRUVAXDXCXFXCFVSVTWAWBWCWDVDXAXDWEKYJYTWFFE
      WGYHYTIXDWEXEXCGOZYHYTWFGXDXCHWHUVLXGYLXRYOYGYSUVLXGYKXCFQZYLUVLXFYKOXGUV
      MWFUVLXFGXCPYKXCGXCWIXCGGWJUGXFYKXCFWKWAXCGYKFWLWMXQYNAXCGXPYMBXCGXOCXCGW
      NWOWOYFYRDXCGYEYQAXCGUVLYDYPYAYCBXCGWPWQWOWRUPWSWTWAWM $.
      $( [3-Nov-2006] $) $( [10-Oct-2006] $)
  $}

  ${
    $d u x y z G $.  $d u x y z U $.  $d u x y z X $.  $d y N $.
    isgrpi.1 $e |- X e. V $.
    isgrpi.2 $e |- G : ( X X. X ) --> X $.
    isgrpi.3 $e |- ( ( x e. X /\ y e. X /\ z e. X ) ->
                   ( ( x G y ) G z ) = ( x G ( y G z ) ) ) $.
    isgrpi.4 $e |- U e. X $.
    isgrpi.5 $e |- ( x e. X -> ( U G x ) = x ) $.
    isgrpi.6 $e |- ( x e. X -> N e. X ) $.
    isgrpi.7 $e |- ( x e. X -> ( N G x ) = U ) $.
    $( Properties that determine a group operation.  Read ` N ` as
       ` N ( x ) ` . $)
    isgrpi $p |- G e. Grp $=
      ( vu cgr wcel cxp wf cv co wceq wral wrex wa w3a rgen3 opreq1 eqeq1d
      rcla4ev sylanc jca rgen eqeq2 rexbidv anbi12d ralbidv mp2an 3pm3.2i cvv
      wb xpex fex crn wfo fooprval eqcomd rcla4eopr mp3an1 mpdan mpbir2an forn
      ax-mp eqcomi isgrp mpbir ) EPQZGGRZGESZATZBTZEUACTZEUAVTWAWBEUAZEUAUBZCGU
      CBGUCAGUCZOTZVTEUAZVTUBZWAVTEUAZWFUBZBGUDZUEZAGUCZOGUDZUFZVSWEWNIWDABCGJU
      GDGQZDVTEUAZVTUBZWIDUBZBGUDZUEZAGUCZWNKXAAGVTGQZWRWTLFGQFVTEUAZDUBZWTXCWS
      XEBFGWAFUBWIXDDWAFVTEUHUIUJMNUKULUMWMXBODGWFDUBZWLXAAGXFWHWRWKWTXFWGWQVTW
      FDVTEUHUIXFWJWSBGWFDWIUNUOUPUQUJURUSEUTQZVQWOVAVSVRUTQXGIGGHHVBVRGUTEVCUR
      ABCOUTEGEVDZGVRGEVEZXHGUBXIVSVTWCUBCGUDBGUDZAGUCBCAGGGEVFIXJAGXCVTWQUBZXJ
      XCWQVTLVGWPXCXKXJKBCGGDVTVTEVHVIVJUMVKVRGEVLVMVNVOVMVP $.
      $( [4-Nov-2006] $) $( [4-Nov-2006] $)
  $}

  ${
    $d w x y z A $.  $d x y z B $.  $d z C $.  $d u w x y z G $.
    $d u w x y z X $.  $d y U $.
    grpfo.1 $e |- X = ran G $.
    $( A group operation maps onto the group's underlying set. $)
    grpfo $p |- ( G e. Grp -> G : ( X X. X ) -onto-> X ) $=
      ( vx vy vz vu cgr wcel cxp wf crn wceq wa wfo cv co wral wrex w3a isgrp
      ibi 3simp1d eqcomi jctir dffo2 sylibr ) AHIZBBJZBAKZALZBMZNUIBAOUHUJULUHU
      JDPZEPZAQFPZAQUMUNUOAQAQMFBREBRDBRZGPZUMAQUMMUNUMAQUQMEBSNDBRGBSZUHUJUPUR
      TDEFGHABCUAUBUCBUKCUDUEUIBAUFUG $.
      $( [31-Oct-2006] $) $( [30-Oct-2006] $)

    $( Closure law for a group operation. $)
    grpcl $p |- ( ( G e. Grp /\ A e. X /\ B e. X ) -> ( A G B ) e. X ) $=
      ( cxp wf wcel co cgr foprrn wfo grpfo fof syl syl3an1 ) DDFZDCGZADHBDHABC
      IDHCJHZABDDDCKSQDCLRCDEMQDCNOP $.
      $( [19-Oct-2006] $) $( [10-Oct-2006] $)

    $( A group has a left identity element, and every member has a left
       inverse. $)
    grplidinv $p |- ( G e. Grp ->
         E. u e. X A. x e. X ( ( u G x ) = x /\ E. y e. X ( y G x ) = u ) ) $=
      ( vz cgr wcel cxp wf cv co wceq wral wrex wa w3a isgrp ibi 3simp3 syl )
      DHIZEEJEDKZALZBLZDMGLZDMUEUFUGDMDMNGEOBEOAEOZCLZUEDMUENUFUEDMUINBEPQAEOCE
      PZRZUJUCUKABGCHDEFSTUDUHUJUAUB $.
      $( [3-Nov-2006] $) $( [2-Nov-2006] $)

    $( The base set of a group is not empty.  (Contributed by Szymon
       Jaroszewicz, 3-Apr-2007.) $)
    grpn0 $p |- ( G e. Grp -> X =/= (/) ) $=
      ( vu vx vy cgr wcel cv co wceq wrex wa wral c0 wne grplidinv rexn0 syl )
      AGHDIZEIZAJUAKFIUAAJTKFBLMEBNZDBLBOPEFDABCQUBDBRS $.
      $( [3-Apr-2007] $) $( [2-Mar-2007] $)

    $( A group operation is associative. $)
    grpass $p |- ( ( G e. Grp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
              ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( vx vy vz vu cgr wcel w3a co wceq cv wral opreq1 opreq1d eqeq12d opreq2
      opreq2d rcla43v cxp wf wrex wa isgrp ibi 3simp2d syl5com imp ) DKLZAELBEL
      CELMZABDNZCDNZABCDNZDNZOZUNGPZHPZDNZIPZDNZUTVAVCDNZDNZOZIEQHEQGEQZUSUMVGU
      SAVADNZVCDNZAVEDNZOUOVCDNZABVCDNZDNZOGHIABCEEEUTAOZVDVJVFVKVOVBVIVCDUTAVA
      DRSUTAVEDRTVABOZVJVLVKVNVPVIUOVCDVABADUASVPVEVMADVABVCDRUBTVCCOZVLUPVNURV
      CCUODUAVQVMUQADVCCBDUAUBTUCUMEEUDEDUEZVHJPZUTDNUTOVAUTDNVSOHEUFUGGEQJEUFZ
      UMVRVHVTMGHIJKDEFUHUIUJUKUL $.
      $( [18-Oct-2006] $) $( [10-Oct-2006] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem1 $p |- ( ( ( G e. Grp /\ ( Y e. X /\ A e. X ) ) /\
                  ( ( Y G A ) = U /\ ( A G A ) = A ) ) -> ( U G A ) = U ) $=
      ( cgr wcel wa co wceq opreq1 ad2antrl w3a grpass pm3.26 pm3.27 3jca
      sylan2 adantr eqtr3d opreq2 ad2antll simprl 3eqtrd ) CGHZEDHZADHZIZIZEACJ
      ZBKZAACJZAKZIZIZBACJZEUMCJZUKBUPUKACJZUQURULUSUQKUJUNUKBACLMUJUSURKZUOUFU
      GUHUHNUTUIEAACDFOUIUGUHUHUGUHPUGUHQZVARSTUAUNURUKKUJULUMAECUBUCUJULUNUDUE
      $.
      $( [24-Oct-2006] $) $( [10-Oct-2006] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem2 $p |- ( ( ( G e. Grp /\ ( Y e. X /\ A e. X ) ) /\ ( ( U G Y )
         = Y /\ ( Y G A ) = U ) ) -> ( ( A G Y ) G ( A G Y ) ) = ( A G Y ) ) $=
      ( cgr wcel wa co wceq w3a simprr simprl grpcl 3com23 3expb 3jca grpass
      syldan adantr opreq1 adantl pm3.26 eqtr2d pm3.27 sylan2 sylan9eqr eqcomd
      opreq2d eqtrd ) CGHZEDHZADHZIZIZBECJZEKZEACJZBKZIZIZAECJZVCCJZAEVCCJZCJZV
      CUPVDVFKZVAULUOUNUMVCDHZLVGUPUNUMVHULUMUNMULUMUNNULUMUNVHULUNUMVHAECDFOPQ
      RAEVCCDFSTUAVBVEEACVBEVEVAUPEUSECJZVEVAVIUQEUTVIUQKURUSBECUBUCURUTUDUEULU
      MUNUMLVIVEKUOEAECDFSUOUMUNUMUMUNUDZUMUNUFVJRUGUHUIUJUK $.
      $( [24-Oct-2006] $) $( [10-Oct-2006] $)

    ${
      $d w x y z U $.  $d w y ph $.  $d y ps $.
      grpidinvlem3.2 $e |- ( ph <-> A. x e. X ( U G x ) = x ) $.
      grpidinvlem3.3 $e |- ( ps <-> A. x e. X E. z e. X ( z G x ) = U ) $.
      $( Lemma for ~ grpidinv . $)
      grpidinvlem3 $p |- ( ( ( ( G e. Grp /\ U e. X ) /\ ( ph /\ ps ) ) /\
                  A e. X ) -> E. y e. X ( ( y G A ) = U /\ ( A G y ) = U ) ) $=
        ( vw cgr wcel wa cv co wceq wrex wral opreq2 eqeq1d rexbidv rcla4cva
        opreq1 cbvrexv ralbii bitr sylanb adantll id eqeq12d rcla4va grpcl
        3expa adantllr biimp ad2antrl ad2antrr sylanc adantr grpidinvlem2
        pm3.22 ancom31s adantlr adantlll anim1i wi 3expb adantrl grpidinvlem1
        anass an1rs ex syldan imp sylan exp43 r19.23adv sylan2b syl5 mpand
        exp32 com34 imp32 exp3a imp31 mpd eqtr3d ancld r19.22dva ) HNOZGIOZPZAB
        PZPZFIOZPZDQZFHRZGSZDITZXBFWTHRZGSZPZDITWPWRXCWOBWRXCAWTCQZHRZGSZDITZCI
        UAZWRXCBXJXCCFIXGFSZXIXBDIXLXHXAGXGFWTHUBUCUDUEBEQZXGHRZGSZEITZCIUAZXKL
        XPXJCIXOXIEDIXMWTSXNXHGXMWTXGHUFUCUGUHUIUJUKUKWSXBXFDIWSWTIOZPZXBXEXSXB
        XEXSXBPZGXDHRZXDGXSYAXDSZXBXDIOZGXGHRZXGSZCIUAZYBXSYEYBCXDIXGXDSZYDYAXG
        XDXGXDGHUBYGULUMUNWOWRXRYCWPWMWRXRYCWNWMWRXRYCFWTHIJUOZUPUQUQWQYFWRXRAY
        FWOBAYFKURUSUTVAVBXTXDXDHRXDSZYAGSZWMXRWRPZPZGWTHRZWTSZXBPYIXTFGHIWTJVC
        XSYLXBWOWRXRYLWPWMWRXRYLWNXRWRWMYLYKWMVDVEUQUQVBXSYNXBWPWRXRYNWOWPXRYNW
        RAXRYNBYFXRYNAYEYNCWTIXGWTSZYDYMXGWTXGWTGHUBYOULUMUEKUJVFVFVGVHVAXSYIYJ
        VIZXBWQWRXRYPWQWRXRYPWOABWRXRPZYPVIWOAYQBYPWOAYQBYPVIWOAYQPZPZYCBYPWMYR
        YCWNWMYQYCAWMWRXRYCYHVJZVKVFYSMQZXDHRZGSZMITZYPYCBPYSUUCYPMIYSUUAIOZUUC
        YIYJWMUUEYCPPZUUCYIPYJYSUUEPXDGHIUUAJVLYSUUEUUFWMYRUUEUUFVIZWNWMYQUUGAW
        MYQYCUUGYTWMYCPUUEUUFWMUUEYCUUFWMUUEPYCPUUFWMUUEYCVMURVNVOVPVKVFVQVRVSV
        TYCUUAXGHRZGSZMITZCIUAZUUDBUUJUUDCXDIYGUUIUUCMIYGUUHUUBGXGXDUUAHUBUCUDU
        NBXQUUKLXPUUJCIXOUUIEMIXMUUASXNUUHGXMUUAXGHUFUCUGUHUIWAWBWCWDWEWFWGWHVB
        WIWJVOWKWLWI $.
        $( [25-Oct-2006] $) $( [11-Oct-2006] $)
    $}

    $( Lemma for ~ grpidinv . $)
    grpidinvlem4 $p |- ( ( ( G e. Grp /\ A e. X ) /\ E. y e. X
             ( ( y G A ) = U /\ ( A G y ) = U ) ) -> ( A G U ) = ( U G A ) ) $=
      ( cgr wcel wa cv co wceq wrex w3a grpass simpll simplr pm3.27 3jca
      sylanc opreq2 sylan9eq eqcomd opreq1 anasss exp31 r19.23adv imp ) DGHZBEH
      ZIZAJZBDKZCLZBULDKZCLZIZAEMBCDKZCBDKZLZUKUQUTAEUKULEHZUQUTUKVAIZUNUPUTVBU
      NIZUPURUOBDKZUSVCVDURVBUNVDBUMDKZURUIUJVAUJNVDVELVBBULBDEFOUIUJVAPVBUJVAU
      JUIUJVAQZUKVARVFSTUMCBDUAUBUCUOCBDUDUBUEUFUGUH $.
      $( [25-Oct-2006] $) $( [14-Oct-2006] $)

    $( A group has a left and right identity element, and every member has a
       left and right inverse. $)
    grpidinv $p |- ( G e. Grp -> E. u e. X A. x e. X ( ( ( u G x ) = x /\
         ( x G u ) = x ) /\ E. y e. X ( ( y G x ) = u /\ ( x G y ) = u ) ) ) $=
      ( vz vw cgr wcel cv co wceq wrex wa wral grplidinv opreq2 id eqeq12d
      rcla4cva pm3.26 r19.20si sylan adantll grpidinvlem4 anim1i pm4.2
      grpidinvlem3 adantrr adantr adantl ad2antlr pm3.27 jca sylanc eqtrd
      r19.21aiva exp32 r19.22dv mpd ) DIJZCKZGKZDLZVDMZHKVDDLVCMHENZOZGEPZCENVC
      AKZDLZVJMZVJVCDLZVJMZOZBKZVJDLVCMVJVPDLVCMOBENZOZAEPZCENGHCDEFQVBVIVSCEVB
      VCEJZVIVSVBVTVIOZOZVRAEWBVJEJZOZVOVQWDVLVNWAWCVLVBVIWCVLVTVFGEPZWCVLVIVFV
      LGVJEVDVJMZVEVKVDVJVDVJVCDRWFSTUAVHVFGEVFVGUBUCZUDUEUEZWDVMVKVJVBWCOVQVMV
      KMWDBVJVCDEFUFWBVBWCVBWAUBUGVBVTOZWEVGGEPZOZOWCVQWDWEWJGBHVJVCDEFWEUHWJUH
      UIWDWIWKWBWIWCVBVTWIVIWISUJUKWDWEWJWAWEVBWCVIWEVTWGULUMWAWJVBWCVIWJVTVHVG
      GEVFVGUNUCULUMUOUOWBWCUNUPZUPWHUQUOWLUOURUSUTVA $.
      $( [25-Oct-2006] $) $( [14-Oct-2006] $)

    $( The left identity element of a group is unique.  Lemma 2.2.1(a) of
       [Herstein] p. 55. $)
    grpideu $p |- ( G e. Grp -> E! u e. X A. x e. X ( u G x ) = x ) $=
      ( vw vz vy cgr wcel cv co wceq wral wi wa wrex wreu grpidinv simpll
      r19.20si opreq2 id eqeq12d cbvralv sylib adantl ad2antlr eqeq1d opreq1
      anbi12d rexbidv rcla4va adantll pm3.27 sylan2 grpidinvlem4 syldan an1rs
      adantllr adantr adantlr adantrl adantrr 3eqtr3d ex mpand r19.21aiva jca
      r19.22dva mpd ralbidv reu8 sylibr ) CIJZBKZAKZCLZVQMZADNZFKZVQCLZVQMZADNZ
      VPWAMZOZFDNZPZBDQZVTBDRVOVPGKZCLZWJMZWJVPCLWJMZPZHKZWJCLZVPMZWJWOCLZVPMZP
      ZHDQZPZGDNZBDQWIGHBCDESVOXCWHBDVOVPDJZPZXCWHXEXCPZVTWGXCVTXEXCWLGDNVTXBWL
      GDWLWMXATUAWLVSGADWJVQMZWKVRWJVQWJVQVPCUBXGUCUDUEUFZUGXFWFFDXFWADJZPZVTWD
      WEXCVTXEXIXHUHXJVTWDPZWEXJXKPWAVPCLZVPWACLZVPWAXJXLXMMZXKVOXCXIXNXDVOXIXC
      XNVOXIPZXCWOWACLZVPMZWAWOCLZVPMZPZHDQZXNXOXAGDNZYAXCXIYBYAVOXAYAGWADWJWAM
      ZWTXTHDYCWQXQWSXSYCWPXPVPWJWAWOCUBUIYCWRXRVPWJWAWOCUJUIUKULUMUNXBXAGDWNXA
      UOUAUPHWAVPCDEUQURUSUTVAXEXIXKXLVPMZXCXEXIPZWDYDVTXEWDYDXIXDWDYDVOWCYDAVP
      DVQVPMZWBXLVQVPVQVPWACUBYFUCUDUMUNVBVCUTXEXIXKXMWAMZXCYEVTYGWDXIVTYGXEVSY
      GAWADVQWAMZVRXMVQWAVQWAVPCUBYHUCUDUMUNVDUTVEVFVGVHVIVFVJVKVTWDBFDWEVSWCAD
      WEVRWBVQVPWAVQCUJUIVLVMVN $.
      $( [25-Oct-2006] $) $( [14-Oct-2006] $)
  $}

  $( The empty set is not a group. $)
  0ngrp $p |- -. (/) e. Grp $=
    ( c0 cgr wcel wne wn wceq eqid nne mpbir crn rn0 eqcomi grpn0 mto ) ABCAADZ
    OEAAFAGAAHIAAAJAKLMN $.
    $( [25-Apr-2007] $) $( [25-Apr-2007] $)

  ${
    grprn.1 $e |- G e. Grp $.
    grprn.2 $e |- G : ( X X. X ) --> X $.
    $( The range of a group operation.  Useful for satisfying ` X = ran G `
       hypothesis for specific groups. $)
    grprn $p |- X = ran G $=
      ( cxp wfn crn wceq wf ffn ax-mp wfo cgr wcel eqid grpfo fof wa fndmu
      xpid11 sylib mp2an ) ABBEZFZAAGZUEEZFZBUEHZUCBAIUDDUCBAJKUFUEAIZUGUFUEALZ
      UIAMNUJCAUEUEOPKUFUEAQKUFUEAJKUDUGRUCUFHUHUCUFASBUETUAUB $.
      $( [6-Nov-2006] $) $( [5-Nov-2006] $)
  $}

  ${
    $d x y A $.  $d g u x y G $.  $d u x y U $.  $d g u x y X $.
    grpidval.1 $e |- X = ran G $.
    grpidval.2 $e |- U = ( Id ` G ) $.
    $( The value of the identity element of a group. $)
    grpidval $p |- ( G e. Grp ->
                   U = U. { u e. X | A. x e. X ( u G x ) = x } ) $=
      ( vg vy cgr wcel cgi cfv cv co wceq wral crab cuni cvv crn rnexg
      syl5eqel rabexg uniexg 3syl rneq syl6eqr rabeq syl raleq1d opreq eqeq1d
      ralbidv bitrd rabbisdv eqtrd unieqd df-gid fvopab4g mpdan syl5eq ) DJKZDL
      MZBNZANZDOZVFPZAEQZBERZSZCVCVKTKZVDVKPVCETKVJTKVLVCDUAZTEDJUBFUCVIBETUDVJ
      TUEUFHIDVEVFHNZOZVFPZAVNUAZQZBVQRZSVKJTLVNDPZVSVJVTVSVRBERZVJVTVQEPVSWAPV
      TVQVMEVNDUGFUHZVRBVQEUIUJVTVRVIBEVTVRVPAEQVIVTVPAVQEWBUKVTVPVHAEVTVOVGVFV
      EVFVNDULUMUNUOUPUQURAIBHUSUTVAGVB $.
      $( [26-Oct-2006] $) $( [24-Oct-2006] $)

    $( The identity element of a group belongs to the group. $)
    grpidcl $p |- ( G e. Grp -> U e. X ) $=
      ( vu vx cgr wcel cv co wceq wral crab cuni grpidval wreu grpideu reucl
      syl eqeltrd ) BHIZAFJGJZBKUCLGCMZFCNOZCGFABCDEPUBUDFCQUECIGFBCDRUDFCSTUA
      $.
      $( [26-Oct-2006] $) $( [24-Oct-2006] $)

    $( A group's properties using the explicit identity element. $)
    grpidinv2 $p |- ( ( G e. Grp /\ A e. X ) -> ( ( ( U G A ) = A /\
         ( A G U ) = A ) /\ E. y e. X ( ( y G A ) = U /\ ( A G y ) = U ) ) ) $=
      ( vx vu cv co wceq wa wrex wral wcel cgr opreq2 id eqeq12d opreq1
      anbi12d eqeq1d rexbidv rcla4cva crab cuni wreu wss wi ssid simpll
      r19.20si a1i rgen reuuniss2 mpanl12 grpidinv grpideu sylanc grpidval
      eqtr4d wb eqeq2 ralbidv reuuni2 grpidcl reuss2 mpbird sylan ) CHJZDKZVKLZ
      VKCDKZVKLZMZAJZVKDKZCLZVKVQDKZCLZMZAENZMZHEOZBEPCBDKZBLZBCDKZBLZMZVQBDKZC
      LZBVQDKZCLZMZAENZMZDQPZWDWQHBEVKBLZVPWJWCWPWSVMWGVOWIWSVLWFVKBVKBCDRWSSZT
      WSVNWHVKBVKBCDUAWTTUBWSWBWOAEWSVSWLWAWNWSVRWKCVKBVQDRUCWSVTWMCVKBVQDUAUCU
      BUDUBUEWRWEIJZVKDKZVKLZVKXADKZVKLZMZVRXALZVTXALZMZAENZMZHEOZIEUFUGZCLZWRX
      MXCHEOZIEUFUGZCXLIENZXOIEUHZXMXPLZWREEUIZXLXOUJZIEOZXQXRMZXSEUKZYAIEYAXAE
      PXKXCHEXCXEXJULUMUNUOZXLXOIEEUPUQHAIDEFURZHIDEFUSZUTHICDEFGVAVBCEPXLIEUHZ
      WEXNVCWRXLWEIECXACLZXKWDHEYIXFVPXJWCYIXCVMXEVOYIXBVLVKXACVKDUAUCYIXDVNVKX
      ACVKDRUCUBYIXIWBAEYIXGVSXHWAXACVRVDXACVTVDUBUDUBVEVFCDEFGVGXQXRYHWRXTYBYC
      YHYDYEXLXOIEEVHUQYFYGUTUTVIVJ $.
      $( [27-Oct-2006] $) $( [24-Oct-2006] $)

    $( The identity element of a group is a left identity. $)
    grplid $p |- ( ( G e. Grp /\ A e. X ) -> ( U G A ) = A ) $=
      ( vy cgr wcel wa co wceq cv wrex grpidinv2 simpll syl ) CHIADIJBACKALZABC
      KALZJGMZACKBLATCKBLJGDNZJRGABCDEFORSUAPQ $.
      $( [28-Oct-2006] $) $( [24-Oct-2006] $)

    $( The identity element of a group is a right identity. $)
    grprid $p |- ( ( G e. Grp /\ A e. X ) -> ( A G U ) = A ) $=
      ( vy cgr wcel wa co wceq cv wrex grpidinv2 simplr syl ) CHIADIJBACKALZABC
      KALZJGMZACKBLATCKBLJGDNZJSGABCDEFORSUAPQ $.
      $( [27-Oct-2006] $) $( [24-Oct-2006] $)
  $}

  ${
    $d x y z G $.  $d x y z X $.
    $d y A $.  $d y B $.  $d y C $.  $d y G $.  $d y X $.
    grprcan.1 $e |- X = ran G $.
    $( Right cancellation law for groups. $)
    grprcan $p |- ( ( G e. Grp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( A G C ) = ( B G C ) <-> A = B ) ) $=
      ( vy cgr wcel co wceq wb wa cv cgi cfv wrex wi eqid grpidinv2 pm3.27
      r19.22si adantl syl adantrl adantlr opreq1 ad2antll grpass 3exp2 imp41
      adantlrl adantrr imp42 adantllr 3eqtr3d adantrrl opreq2 ad2antrl grprid
      ad2antrr ad2ant2r adantr exp45 r19.23adv mpd impbid1 exp43 3imp2 ) DHIZAE
      IZBEIZCEIZACDJZBCDJZKZABKZLZVJVKVLVMVRVJVKMZVLVMMZMZVPVQWACGNZDJZDOPZKZGE
      QZVPVQRZVJVTWFVKVJVMWFVLVJVMMWDCDJCKCWDDJCKMZWBCDJWDKZWEMZGEQZMWFGCWDDEFW
      DSZTWKWFWHWJWEGEWIWEUAUBUCUDUEUFWAWEWGGEWAWBEIZWEVPVQWAWMWEVPMMZMZAWDDJZB
      WDDJZABWOAWCDJZBWCDJZWPWQWAWMVPWRWSKWEWAWMVPMMVNWBDJZVOWBDJZWRWSVPWTXAKWA
      WMVNVOWBDUGUHWAWMWTWRKZVPVSVMWMXBVLVJVKVMWMXBVJVKVMWMXBACWBDEFUIUJUKULUMW
      AWMXAWSKZVPVJVTWMXCVKVJVLVMWMXCVJVLVMWMXCBCWBDEFUIUJUNUOUMUPUQWNWRWPKZWAW
      EXDWMVPWCWDADURUSUCWNWSWQKZWAWEXEWMVPWCWDBDURUSUCUPVSWPAKVTWNAWDDEFWLUTVA
      WAWQBKZWNVJVLXFVKVMBWDDEFWLUTVBVCUPVDVEVFABCDUGVGVHVI $.
      $( [28-Oct-2006] $) $( [26-Oct-2006] $)
  $}

  ${
    $d y z A $.  $d y z G $.  $d y z U $.  $d y z X $.
    grpinveu.1 $e |- X = ran G $.
    grpinveu.2 $e |- U = ( Id ` G ) $.
    $( The left inverse element of a group is unique.  Lemma 2.2.1(b) of
       [Herstein] p. 55. $)
    grpinveu $p |- ( ( G e. Grp /\ A e. X ) -> E! y e. X ( y G A ) = U ) $=
      ( vz cgr wcel wa cv co wceq wi wral wrex wreu grpidinv2 pm3.26 r19.22si
      adantl syl w3a grprcan eqtr3t syl5bi 3exp2 com24 imp41 an1rs exp3a imp
      r19.21aiva ex ancld r19.22dva mpd opreq1 eqeq1d reu8 sylibr ) DIJZBEJZKZA
      LZBDMZCNZHLZBDMZCNZVFVINZOZHEPZKZAEQZVHAERVEVHAEQZVPVECBDMBNBCDMBNKZVHBVF
      DMCNZKZAEQZKVQABCDEFGSWAVQVRVTVHAEVHVSTUAUBUCVEVHVOAEVEVFEJZKZVHVNWCVHVNW
      CVHKVMHEWCVIEJZVHVMWCWDKZVHVMWEVHVKVLVEWDWBVHVKKZVLOZVCVDWDWBWGVCWBWDVDWG
      VCWBWDVDWGVCWBWDVDUDKVGVJNVLWFVFVIBDEFUEVGVJCUFUGUHUIUJUKULUMUKUNUOUPUQUR
      VHVKAHEVLVGVJCVFVIBDUSUTVAVB $.
      $( [29-Oct-2006] $) $( [27-Oct-2006] $)
  $}

  ${
    $d n x y A $.  $d y B $.  $d y C $.  $d f g n x y G $.  $d f g n x y X $.
    $d f g n x U $.
    grpinvfval.1 $e |- X = ran G $.
    grpinvfval.2 $e |- U = ( Id ` G ) $.
    grpinvfval.3 $e |- N = ( inv ` G ) $.
    $( The inverse function of a group. $)
    grpinvfval $p |- ( G e. Grp -> N = { <. x , n >. | ( x e. X /\
                     n = U. { y e. X | ( y G x ) = U } ) } ) $=
      ( vg vf cgr wcel cgn cfv cv co wceq crab cuni wa copab cvv crn rnexg
      syl5eqel funopabex2g syl cgi rneq syl6eqr eleq2d rabeq opreq fveq2
      eqeq12d rabbisdv eqtrd unieqd eqeq2d anbi12d opabbidv df-ginv fvopab4g
      mpdan syl5eq ) EMNZEOPZAQZGNZDQZBQZVJERZCSZBGTZUAZSZUBZADUCZFVHVTUDNZVIVT
      SVHGUDNWAVHEUEZUDGEMUFHUGADGVQUDUHUIKLEVJKQZUEZNZVLVMVJWCRZWCUJPZSZBWDTZU
      AZSZUBZADUCVTMUDOWCESZWLVSADWMWEVKWKVRWMWDGVJWMWDWBGWCEUKHULZUMWMWJVQVLWM
      WIVPWMWIWHBGTZVPWMWDGSWIWOSWNWHBWDGUNUIWMWHVOBGWMWFVNWGCVMVJWCEUOWMWGEUJP
      CWCEUJUPIULUQURUSUTVAVBVCADBLKVDVEVFJVG $.
      $( [30-Oct-2006] $) $( [26-Oct-2006] $)

    $( The inverse of a group element. $)
    grpinvval $p |- ( ( G e. Grp /\ A e. X ) ->
                    ( N ` A ) = U. { y e. X | ( y G A ) = U } ) $=
      ( vx vn cgr wcel wa cfv cv co wceq crab cuni copab grpinvfval fveq1d
      adantr cvv opreq2 eqeq1d rabbisdv unieqd eqid fvopab4g pm3.27 crn rnexg
      syl5eqel rabexg uniexg 3syl sylanc eqtrd ) DLMZBFMZNZBEOZBJPZFMKPAPZVEDQZ
      CRZAFSZTZRNJKUAZOZVFBDQZCRZAFSZTZVAVDVLRVBVABEVKJACKDEFGHIUBUCUDVBVPUEMZV
      LVPRVCJKBVJVPFUEVKVEBRZVIVOVRVHVNAFVRVGVMCVEBVFDUFUGUHUIVKUJUKVAVBULVAVQV
      BVAFUEMVOUEMVQVADUMUEFDLUNGUOVNAFUEUPVOUEUQURUDUSUT $.
      $( [30-Oct-2006] $) $( [26-Oct-2006] $)
  $}

  ${
    $d y A $.  $d y G $.  $d y X $.
    grpinvcl.1 $e |- X = ran G $.
    grpinvcl.2 $e |- N = ( inv ` G ) $.
    $( A group element's inverse is a group element. $)
    grpinvcl $p |- ( ( G e. Grp /\ A e. X ) -> ( N ` A ) e. X ) $=
      ( vy cgr wcel wa cfv cv co cgi wceq crab cuni eqid grpinvval wreu
      grpinveu reucl syl eqeltrd ) BHIADIJZACKGLABMBNKZOZGDPQZDGAUFBCDEUFRZFSUE
      UGGDTUHDIGAUFBDEUIUAUGGDUBUCUD $.
      $( [31-Oct-2006] $) $( [27-Oct-2006] $)
  $}

  ${
    $d y A $.  $d y G $.  $d y N $.  $d y U $.  $d y X $.
    grpinv.1 $e |- X = ran G $.
    grpinv.2 $e |- U = ( Id ` G ) $.
    grpinv.3 $e |- N = ( inv ` G ) $.
    $( The properties of a group element's inverse. $)
    grpinv $p |- ( ( G e. Grp /\ A e. X ) ->
                 ( ( ( N ` A ) G A ) = U /\ ( A G ( N ` A ) ) = U ) ) $=
      ( vy cgr wcel wa cfv co wceq cv crab cuni wrex wreu wss wi wral ssid
      pm3.26 a1i rgen reuuniss2 mpanl12 grpidinv2 pm3.27d grpinveu sylanc
      grpinvval eqtr4d wb opreq1 eqeq1d opreq2 anbi12d reuuni2 grpinvcl reuss2
      mpbird ) CJKAEKLZADMZACNZBOZAVFCNZBOZLZIPZACNZBOZAVLCNZBOZLZIEQRZVFOZVEVR
      VNIEQRZVFVQIESZVNIETZVRVTOZVEEEUAZVQVNUBZIEUCZWAWBLZWCEUDZWEIEWEVLEKVNVPU
      EUFUGZVQVNIEEUHUIVEBACNAOABCNAOLWAIABCEFGUJUKZIABCEFGULZUMIABCDEFGHUNUOVF
      EKVQIETZVKVSUPVEVQVKIEVFVLVFOZVNVHVPVJWMVMVGBVLVFACUQURWMVOVIBVLVFACUSURU
      TVAACDEFHVBWAWBWLVEWDWFWGWLWHWIVQVNIEEVCUIWJWKUMUMVD $.
      $( [1-Nov-2006] $) $( [27-Oct-2006] $)

    $( The left inverse of a group element. $)
    grplinv $p |- ( ( G e. Grp /\ A e. X ) -> ( ( N ` A ) G A ) = U ) $=
      ( cgr wcel wa cfv co wceq grpinv pm3.26d ) CIJAEJKADLZACMBNAQCMBNABCDEFGH
      OP $.
      $( [4-Nov-2006] $) $( [27-Oct-2006] $)

    $( The right inverse of a group element. $)
    grprinv $p |- ( ( G e. Grp /\ A e. X ) -> ( A G ( N ` A ) ) = U ) $=
      ( cgr wcel wa cfv co wceq grpinv pm3.27d ) CIJAEJKADLZACMBNAQCMBNABCDEFGH
      OP $.
      $( [6-Nov-2006] $) $( [27-Oct-2006] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid1 $p |- ( ( G e. Grp /\ A e. X /\ B e. X ) ->
                    ( ( N ` A ) = B <-> ( A G B ) = U ) ) $=
      ( cgr wcel w3a cfv wceq co wa opreq2 adantl grprinv 3adant3 adantr
      eqtr3d grplinv opreq1d grpinvcl adantrr simprl simprr 3jca grpass syldan
      3impb grplid 3adant2 3eqtr3d grprid 3eqtr3rd impbida ) DJKZAFKZBFKZLZAEMZ
      BNZABDOZCNZVBVDPAVCDOZVECVDVGVENVBVCBADQRVBVGCNZVDUSUTVHVAACDEFGHISTUAUBV
      BVFPVCVEDOZVCCDOZBVCVFVIVJNVBVECVCDQRVBVIBNVFVBVCADOZBDOZCBDOZVIBUSUTVLVM
      NVAUSUTPVKCBDACDEFGHIUCUDTUSUTVAVLVINZUSUTVAPZVCFKZUTVALVNUSVOPVPUTVAUSUT
      VPVAADEFGIUEZUFUSUTVAUGUSUTVAUHUIVCABDFGUJUKULUSVAVMBNUTBCDFGHUMUNUOUAVBV
      JVCNZVFUSUTVRVAUSUTVPVRVQVCCDFGHUPUKTUAUQUR $.
      $( [10-Nov-2006] $) $( [27-Oct-2006] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid2 $p |- ( ( G e. Grp /\ A e. X /\ B e. X ) ->
                    ( ( N ` A ) = B <-> ( B G A ) = U ) ) $=
      ( cgr wcel w3a cfv wceq co wa opreq1 adantl grplinv 3adant3 adantr
      eqtr3d grpinvcl grplid syldan eqcomd simprr simprl adantrr 3jca grpass
      3impb grprinv opreq2d grprid 3adant2 3eqtrd 3eqtr2d impbida ) DJKZAFKZBFK
      ZLZAEMZBNZBADOZCNZVCVEPVDADOZVFCVEVHVFNVCVDBADQRVCVHCNZVEUTVAVIVBACDEFGHI
      STUAUBVCVGPVDCVDDOZVFVDDOZBVCVDVJNVGVCVJVDUTVAVJVDNZVBUTVAVDFKZVLADEFGIUC
      ZVDCDFGHUDUETUFUAVGVKVJNVCVFCVDDQRVCVKBNVGVCVKBAVDDOZDOZBCDOZBUTVAVBVKVPN
      ZUTVAVBPZVBVAVMLVRUTVSPVBVAVMUTVAVBUGUTVAVBUHUTVAVMVBVNUIUJBAVDDFGUKUEULU
      TVAVPVQNVBUTVAPVOCBDACDEFGHIUMUNTUTVBVQBNVABCDFGHUOUPUQUAURUS $.
      $( [23-Nov-2006] $) $( [27-Oct-2006] $)

    $( The inverse of the identity element of a group. $)
    grpinvid $p |- ( G e. Grp -> ( N ` U ) = U ) $=
      ( cgr wcel cfv wceq co grpidcl grplid mpdan wb grpinvid1 mpd3an mpbird )
      BHIZACJAKZAABLAKZTADIZUBABDEFMZAABDEFNOTUCUCUAUBPUDUDAAABCDEFGQRS $.
      $( [5-Dec-2006] $) $( [4-Dec-2006] $)
  $}

  ${
    grplcan.1 $e |- X = ran G $.
    $( Left cancellation law for groups. $)
    grplcan $p |- ( ( G e. Grp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( C G A ) = ( C G B ) <-> A = B ) ) $=
      ( cgr wcel w3a wa co wceq wi cgn cfv opreq2 adantl cgi eqid grplinv
      adantlr opreq1d grpinvcl adantrl simprr simprl 3jca grpass syldan
      anassrs grplid adantr 3eqtr3d adantrr ex exp43 3imp2 impbid1 ) DGHZAEHZBE
      HZCEHZIJCADKZCBDKZLZABLZUSUTVAVBVEVFMZUSUTVAVBVGUSUTJZVAVBJZJZVEVFVJVEJCD
      NOZOZVCDKZVLVDDKZABVEVMVNLVJVCVDVLDPQVJVMALZVEVHVBVOVAVHVBJZVLCDKZADKZDRO
      ZADKZVMAVPVQVSADUSVBVQVSLZUTCVSDVKEFVSSZVKSZTZUAUBUSUTVBVRVMLZUSUTVBJZVLE
      HZVBUTIWEUSWFJWGVBUTUSVBWGUTCDVKEFWCUCZUDUSUTVBUEUSUTVBUFUGVLCADEFUHUIUJV
      HVTALVBAVSDEFWBUKULUMUDULVJVNBLZVEUSVIWIUTUSVIJZVQBDKZVSBDKZVNBWJVQVSBDUS
      VBWAVAWDUDUBUSVIWGVBVAIWKVNLWJWGVBVAUSVBWGVAWHUDUSVAVBUEUSVAVBUFUGVLCBDEF
      UHUIUSVAWLBLVBBVSDEFWBUKUNUMUAULUMUOUPUQABCDPUR $.
      $( [11-Nov-2006] $) $( [27-Oct-2006] $)
  $}

  ${
    $d u v w x y z G $.  $d u v w x y z X $.
    isgrp2i.1 $e |- X e. V $.
    isgrp2i.2 $e |- X =/= (/) $.
    isgrp2i.3 $e |- G : ( X X. X ) --> X $.
    isgrp2i.4 $e |- ( ( x e. X /\ y e. X /\ z e. X ) ->
                    ( ( x G y ) G z ) = ( x G ( y G z ) ) ) $.
    isgrp2i.5 $e |- ( ( x e. X /\ y e. X ) -> E. z e. X ( z G x ) = y ) $.
    isgrp2i.6 $e |- ( ( x e. X /\ y e. X ) -> E. z e. X ( x G z ) = y ) $.
    $( An alternate way to show a group operation.  Exercise 1 of [Herstein]
       p. 57. $)
    isgrp2i $p |- G e. Grp $=
      ( vu vw vv cgr wcel cxp wf cv co wceq wral wrex wa w3a rgen3 wex c0 wne
      ne0 mpbi wi weq eleq1 anbi1d opreq2 eqeq1d rexbidv imbi12d anbi2d eqeq2
      chvarv anidms opreq1 cbvrexv sylib vtocl2ga adantlr adantr 3anbi1d
      opreq1d eqeq12d 3anbi2d opreq2d 3anbi3d 3com12 3expa ad2antlr eqtr3d
      adantl pm3.27 3eqtr3d exp31 r19.23adv mpd ancoms adantll jca com23
      r19.21adv r19.22dva 19.23aiv ax-mp 3pm3.2i cvv wb xpex fex mp2an crn wfo
      fooprval eqcom rexbii r19.21aiva r19.2z mpan syl rgen mpbir2an forn
      eqcomi isgrp mpbir ) DOPZEEQZEDRZASZBSZDTZCSZDTZXRXSYADTZDTZUAZCEUBBEUBAE
      UBZLSZXRDTZXRUAZXSXRDTZYGUAZBEUCZUDZAEUBZLEUCZUEZXQYFYOHYEABCEIUFMSZEPZMU
      GZYOEUHUIZYSGMEUJUKYRYOMYRYGYQDTZYQUAZLEUCZYOYRYAYQDTZYQUAZCEUCZUUCYRUUFX
      REPZYRUDZYAXRDTZYQUAZCEUCZULZYRYRUDZUUFULAMAMUMZUUHUUMUUKUUFUUNUUGYRYRXRY
      QEUNUOUUNUUJUUECEUUNUUIUUDYQXRYQYADUPUQURUSUUGXSEPZUDZUUIXSUAZCEUCZULZUUL
      BMBMUMZUUPUUHUURUUKUUTUUOYRUUGXSYQEUNZUTUUTUUQUUJCEXSYQUUIVAURUSJVBVBVCUU
      EUUBCLEYAYGUAUUDUUAYQYAYGYQDVDUQVEVFYRUUBYNLEYRYGEPZUDZUUBYMAEUVCUUGUUBYM
      UVCUUGUUBYMUVCUUGUDZUUBUDZYIYLUVEYQNSZDTZXRUAZNEUCZYIUVDUVIUUBYRUUGUVIUVB
      YRUUGUDYQYADTZXRUAZCEUCZUVIXRYADTZXSUAZCEUCZUVJXSUAZCEUCUVLABYQXREEUUNUVN
      UVPCEUUNUVMUVJXSXRYQYADVDUQURBAUMUVPUVKCEXSXRUVJVAURKVGUVKUVHCNECNUMZUVJU
      VGXRYAUVFYQDUPUQVEVFVHVIUVCUUBUVIYIULUUGUVCUUBUDZUVHYINEUVRUVFEPZUVHYIUVR
      UVSUDZUVHUDYGUVGDTZUVGYHXRUVTUWAUVGUAUVHUVTUUAUVFDTZUWAUVGUVCUVSUWBUWAUAZ
      UUBYRUVBUVSUWCUVBYRUVSUWCUUGYRUVSUEZXRYQDTZUVFDTZXRUVGDTZUAZULZUVBYRUVSUE
      ZUWCULALALUMZUWDUWJUWHUWCUWKUUGUVBYRUVSXRYGEUNVJUWKUWFUWBUWGUWAUWKUWEUUAU
      VFDXRYGYQDVDVKXRYGUVGDVDVLUSUUGUUOUVSUEZXTUVFDTZXRXSUVFDTZDTZUAZULZUWIBMU
      UTUWLUWDUWPUWHUUTUUOYRUUGUVSUVAVMUUTUWMUWFUWOUWGUUTXTUWEUVFDXSYQXRDUPVKUU
      TUWNUVGXRDXSYQUVFDVDVNVLUSUUGUUOYAEPZUEZYEULUWQCNUVQUWSUWLYEUWPUVQUWRUVSU
      UGUUOYAUVFEUNVOUVQYBUWMYDUWOYAUVFXTDUPUVQYCUWNXRDYAUVFXSDUPVNVLUSIVBVBVBV
      PVQVHUUBUWBUVGUAUVCUVSUUAYQUVFDVDVRVSVIUVHUWAYHUAUVTUVGXRYGDUPVTUVTUVHWAW
      BWCWDVHWEUVDYLUUBUVBUUGYLYRUUGUVBYLUUGUVBUDZUUIYGUAZCEUCZYLUUSUWTUXBULBLB
      LUMZUUPUWTUURUXBUXCUUOUVBUUGXSYGEUNUTUXCUUQUXACEXSYGUUIVAURUSJVBUXAYKCBEC
      BUMUUIYJYGYAXSXRDVDUQVEVFWFWGVIWHWCWIWJWKWEWLWMWNDWOPZXOYPWPXQXPWOPUXDHEE
      FFWQXPEWODWRWSABCLWODEDWTZEXPEDXAZUXEEUAUXFXQXSUVMUAZCEUCZAEUCZBEUBACBEEE
      DXBHUXIBEUUOUXHAEUBZUXIUUOUXHAEUUOUUGUDUVOUXHUUGUUOUVOKWFUVNUXGCEUVMXSXCX
      DVFXEYTUXJUXIGUXHAEXFXGXHXIXJXPEDXKWMXLXMWMXN $.
      $( [5-Mar-2007] $) $( [5-Dec-2006] $)
  $}

  ${
    grp2inv.1 $e |- X = ran G $.
    grp2inv.2 $e |- N = ( inv ` G ) $.
    $( Double inverse law for groups.  Lemma 2.2.1(c) of [Herstein] p. 55. $)
    grp2inv $p |- ( ( G e. Grp /\ A e. X ) -> ( N ` ( N ` A ) ) = A ) $=
      ( cgr wcel wa cfv co wceq cgi grpinvcl eqid grprinv syldan grplinv
      eqtr4d w3a wb pm3.27 3jca grplcan mpbid ) BGHZADHZIZACJZUICJZBKZUIABKZLZU
      JALZUHUKBMJZULUFUGUIDHZUKUOLABCDEFNZUIUOBCDEUOOZFPQAUOBCDEURFRSUFUGUJDHZU
      GUPTUMUNUAUHUSUGUPUFUGUPUSUQUIBCDEFNQUFUGUBUQUCUJAUIBDEUDQUE $.
      $( [24-Nov-2006] $) $( [27-Oct-2006] $)

    $( The inverse of the group operation reverses the arguments.  Lemma
       2.2.1(d) of [Herstein] p. 55. $)
    grpinvop $p |- ( ( G e. Grp /\ A e. X /\ B e. X ) ->
                   ( N ` ( A G B ) ) = ( ( N ` B ) G ( N ` A ) ) ) $=
      ( cgr wcel w3a co cfv wceq cgi grpass 3simp1 3simp2 3simp3 grpcl
      grpinvcl 3adant2 3adant3 syl3anc 3jca sylanc eqid grprinv opreq1d grplid
      syldan 3eqtr3d opreq2d 3eqtrd wb grpinvid1 mpbird ) CHIZAEIZBEIZJZABCKZDL
      BDLZADLZCKZMZVAVDCKZCNLZMZUTVFABVDCKZCKZAVCCKZVGUQURUSVDEIZJVFVJMUTABVDCE
      FOUQURUSPZUTURUSVLUQURUSQUQURUSRZUQVBEIZVCEIZVLUTVBVCCEFSVMUQUSVOURBCDEFG
      TUAZUQURVPUSACDEFGTZUBZUCZUDUEUTVIVCACUTBVBCKZVCCKZVGVCCKZVIVCUTWAVGVCCUQ
      USWAVGMURBVGCDEFVGUFZGUGUAUHUQUSVOVPJWBVIMUTBVBVCCEFOVMUTUSVOVPVNVQVSUDUE
      UQURWCVCMZUSUQURVPWEVRVCVGCEFWDUIUJUBUKULUQURVKVGMUSAVGCDEFWDGUGUBUMUQVAE
      IVLVEVHUNUTVAVDVGCDEFWDGUOVMABCEFSVTUCUP $.
      $( [24-Nov-2006] $) $( [27-Oct-2006] $)
  $}

