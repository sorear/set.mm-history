$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Group homomorphism
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F x y $.  $d G a b x y $.  $d H a b x y $.  $d O x y $.  $d X x y $.
    $d Y a b x y $.
    ghgrpi.1 $e |- G e. GrpOp $.
    ghgrpi.2 $e |- X = ran G $.
    ghgrpi.3 $e |- F : X -onto-> Y $.
    ghgrpi.4 $e |- Y C_ A $.
    ghgrpi.5 $e |- O Fn ( A X. A ) $.
    ghgrpi.6 $e |- ( ( x e. X /\ y e. X ) ->
                     ( F ` ( x G y ) ) = ( ( F ` x ) O ( F ` y ) ) ) $.
    ghgrpi.7 $e |- H = ( O |` ( Y X. Y ) ) $.

    ${
      $d C a b $.  $d D b $.  $d F a b $.  $d O a b $.  $d X a b $.
      $( Lemma for ~ ghgrpi . $)
      ghgrpilem1 $p |- ( ( C e. X /\ D e. X ) ->
                         ( F ` ( C G D ) ) = ( ( F ` C ) H ( F ` D ) ) ) $=
        ( co cfv va vb wcel wa cv wceq wral rgen2a opreq1 fveq2 opreq1d eqeq12d
        fveq2d opreq2 opreq2d cbvral2v mpbi rcla42v mpi crn wfn simpli fnfvelrn
        wfo df-fo mpan simpri syl6eleq cres opreqi oprvres syl5eq syl2an eqtr4d
        cxp ) DJUCZEJUCZUDZDEGSZFTZDFTZEFTZISZWAWBHSZVRUAUEZUBUEZGSZFTZWEFTZWFF
        TZISZUFZUBJUGUAJUGZVTWCUFZAUEZBUEZGSZFTZWOFTZWPFTZISZUFZBJUGAJUGWMXBABJ
        QUHXBWLWEWPGSZFTZWIWTISZUFABUAUBJJWOWEUFZWRXDXAXEXFWQXCFWOWEWPGUIUMXFWS
        WIWTIWOWEFUJUKULWPWFUFZXDWHXEWKXGXCWGFWPWFWEGUNUMXGWTWJWIIWPWFFUJUOULUP
        UQWLWNDWFGSZFTZWAWJISZUFUAUBDEJJWEDUFZWHXIWKXJXKWGXHFWEDWFGUIUMXKWIWAWJ
        IWEDFUJUKULWFEUFZXIVTXJWCXLXHVSFWFEDGUNUMXLWJWBWAIWFEFUJUOULURUSVPWAKUC
        ZWBKUCZWDWCUFVQVPWAFUTZKFJVAZVPWAXOUCXPXOKUFZJKFVDXPXQUDNJKFVEUQZVBZJDF
        VCVFXPXQXRVGZVHVQWBXOKXPVQWBXOUCXSJEFVCVFXTVHXMXNUDWDWAWBIKKVOVIZSWCHYA
        WAWBRVJWAWBKKIVKVLVMVN $.
        $( [25-Apr-2008] $)
    $}

    ${
      $d C w $.  $d F w $.  $d X w $.  $d ch w $.  $d ph w $.
      ghgrpilem2.8 $e |- ( ( w e. X /\ ph ) -> ps ) $.
      ghgrpilem2.9 $e |- ( ( F ` w ) = C -> ( ps <-> ch ) ) $.
      $( Lemma for ~ ghgrpi . $)
      ghgrpilem2 $p |- ( ( ph /\ C e. Y ) -> ch ) $=
        ( wcel cv cfv wceq wrex crn wfn wfo wa df-fo mpbi simpri eleq2i fvelrnb
        wb simpli ax-mp bitr3i ancoms syl5ibcom rexlimdva syl5bi imp ) AHNUDZCV
        GFUEZIUFHUGZFMUHZACVGHIUIZUDZVJVKNHIMUJZVKNUGZMNIUKVMVNULQMNIUMUNZUOUPV
        MVLVJURVMVNVOUSFMHIUQUTVAAVICFMAVHMUDZULBVICVPABUBVBUCVCVDVEVF $.
        $( [25-Apr-2008] $)
    $}

    ${
      $d D c $.  $d F c z $.  $d G c $.  $d H a b c x y z $.  $d N c $.
      $d X z $.  $d Y c z $.
      ghgrpilem3.8 $e |- U = ( Id ` G ) $.
      ghgrpilem3.9 $e |- N = ( inv ` G ) $.
      ghgrpilem3.10 $e |- D = ( /g ` G ) $.
      $( Lemma for ~ ghgrpi . $)
      ghgrpilem3 $p |- ( ( a e. Y /\ b e. Y ) ->
          ( E. c e. Y ( c H a ) = b /\ E. c e. Y ( a H c ) = b ) ) $=
        ( cv wcel wa co wceq wrex cfv crn wfn df-fo mpbi simpli grpdivcl mp3an1
        wfo fnfvelrn simpri syl6eleq ghgrpilem1 sylancom grpnpcan fveq2d eqtr3d
        sylancr opreq1 eqeq1d rcla4ev syl11anc ancoms opreq2 rexbidv ghgrpilem2
        cgr eqeq2 grpoinvcl mpan grpocl sylan syldan grpasscan1 jca ) MUFZLUGZN
        UFZLUGUHOUFZWGHUIZWIUJZOLUKZWGWJHUIZWIUJZOLUKZWHWKBUFZFULZUJZOLUKZWMABB
        CWIFGHJKLPQRSTUAUBWQKUGZWJAUFZFULZHUIZWRUJZOLUKZWTABACWGFGHJKLPQRSTUAUB
        XAXBKUGZXFXAXGUHZWQXBDUIZFULZLUGXJXCHUIZWRUJZXFXHXJFUMZLXHFKUNZXIKUGZXJ
        XMUGXNXMLUJZKLFUTXNXPUHRKLFUOUPZUQZGVRUGZXAXGXOPWQXBDGKQUEURUSZKXIFVAVI
        XNXPXQVBZVCXHXIXBGUIZFULZXKWRXAXGXOYCXKUJXTABCXIXBFGHJKLPQRSTUAUBVDVEXH
        YBWQFXSXAXGYBWQUJPWQXBDGKQUEVFUSVGVHXEXLOXJLWJXJUJXDXKWRWJXJXCHVJVKVLVM
        VNXCWGUJZXEWSOLYDXDWKWRXCWGWJHVOVKVPVQWRWIUJZWSWLOLWRWIWKVSVPVQWHWNWRUJ
        ZOLUKZWPABBCWIFGHJKLPQRSTUAUBXAXCWJHUIZWRUJZOLUKZYGABACWGFGHJKLPQRSTUAU
        BXGXAUHZXBIULZWQGUIZFULZLUGXCYNHUIZWRUJZYJYKYNXMLYKXNYMKUGZYNXMUGXRXGYL
        KUGZXAYQXSXGYRPXBGIKQUDVTWAXSYRXAYQPYLWQGKQWBUSWCZKYMFVAVIYAVCYKXBYMGUI
        ZFULZYOWRXGXAYQUUAYOUJYSABCXBYMFGHJKLPQRSTUAUBVDWDYKYTWQFXSXGXAYTWQUJPX
        BWQGIKQUDWEUSVGVHYIYPOYNLWJYNUJYHYOWRWJYNXCHVOVKVLVMYDYIYFOLYDYHWNWRXCW
        GWJHVJVKVPVQYEYFWOOLWRWIWNVSVPVQWF $.
        $( [25-Apr-2008] $)

      $( Lemma for ~ ghgrpi . $)
      ghgrpilem4 $p |- H e. GrpOp $=
        ( va vb vc vz cvv wfo crn cgr rnexg ax-mp eqeltri fornex mp2 cfv c0 wne
        wcel wfn wceq wa df-fo mpbi simpli grpoidcl fnfvelrn mp2an eleqtri ne0i
        simpri cxp wf cv wral ffnoprv cres wss xpss12 fnssres fneq1i ghgrpilem1
        co mpbir grpocl mp3an1 sylancr eqeltrrd opreq1 eleq1d ghgrpilem2 opreq2
        syl6eleq rgen2a mpbir2an grpoass mpan fveq2d sylan 3impa sylan2 3eqtr3d
        w3a 3impb 3adant3 opreq1d 3adant1 opreq2d 3expb eqeq12d wrex ghgrpilem3
        anasss simpld simprd isgrp2i ) UCUDUEHLKUGUSKLFUHZLUGUSKGUIZUGNGUJUSZXR
        UGUSMGUJUKULUMOKLUGFUNUOEFUPZLUSLUQURXTFUIZLFKUTZEKUSZXTYAUSYBYALVAZXQY
        BYDVBOKLFVCVDZVEZXSYCMEGKNTVFULKEFVGVHYBYDYEVKZVILXTVJULLLVLZLHVMHYHUTZ
        UCVNZUDVNZHWCZLUSZUDLVOUCLVOUCUDLLLHVPYIJYHVQZYHUTZJCCVLZUTYHYPVRZYOQLC
        VRZYRYQPPLCLCVSVHYPYHJVTVHYHHYNSWAWDYMUCUDLYJLUSZYJBVNZFUPZHWCZLUSZYMAB
        BCYKFGHJKLMNOPQRSYTKUSZAVNZFUPZUUAHWCZLUSUUCABACYJFGHJKLMNOPQRSUUEKUSZU
        UDVBZUUGYALUUIUUEYTGWCZFUPZUUGYAABCUUEYTFGHJKLMNOPQRSWBZUUIYBUUJKUSZUUK
        YAUSYFXSUUHUUDUUMMUUEYTGKNWEWFZKUUJFVGWGWHYGWMUUFYJVAZUUGUUBLUUFYJUUAHW
        IZWJWKUUAYKVAZUUBYLLUUAYKYJHWLZWJWKWNWOYSYKLUSZUEVNZLUSYLUUTHWCZYJYKUUT
        HWCZHWCZVAZYSUUSVBZYLUFVNZFUPZHWCZYJYKUVGHWCZHWCZVAZUVDABUFCUUTFGHJKLMN
        OPQRSUVFKUSZYSUUSUVKUVLYSVBUUBUVGHWCZYJUUAUVGHWCZHWCZVAZUVKABBCYKFGHJKL
        MNOPQRSUUDUVLYSUVPUUDUVLVBZUUGUVGHWCZUUFUVNHWCZVAZUVPABACYJFGHJKLMNOPQR
        SUUHUUDUVLUVTUUHUUDUVLXCZUUKUVGHWCZUUFYTUVFGWCZFUPZHWCZUVRUVSUWAUUJUVFG
        WCZFUPZUUEUWCGWCZFUPZUWBUWEUWAUWFUWHFXSUWAUWFUWHVAMUUEYTUVFGKNWPWQWRUUH
        UUDUVLUWGUWBVAZUUIUUMUVLUWJUUNABCUUJUVFFGHJKLMNOPQRSWBWSWTUUHUUDUVLUWIU
        WEVAZUVQUUHUWCKUSZUWKXSUUDUVLUWLMYTUVFGKNWEWFABCUUEUWCFGHJKLMNOPQRSWBXA
        XDXBUWAUUKUUGUVGHUUHUUDUUKUUGVAUVLUULXEXFUWAUWDUVNUUFHUUDUVLUWDUVNVAUUH
        ABCYTUVFFGHJKLMNOPQRSWBXGXHXBXIUUOUVRUVMUVSUVOUUOUUGUUBUVGHUUPXFUUFYJUV
        NHWIXJWKXMUUQUVMUVHUVOUVJUUQUUBYLUVGHUURXFUUQUVNUVIYJHUUAYKUVGHWIXHXJWK
        XMUVGUUTVAZUVHUVAUVJUVCUVGUUTYLHWLUWMUVIUVBYJHUVGUUTYKHWLXHXJWKWTUVEUUT
        YJHWCYKVAUELXKZYJUUTHWCYKVAUELXKZABCDEFGHIJKLUCUDUEMNOPQRSTUAUBXLZXNUVE
        UWNUWOUWPXOXP $.
        $( [25-Apr-2008] $)
    $}

    $( The image of a group ` G ` under a group homomorphism ` F ` is a group,
       and furthermore is Abelian if ` G ` is Abelian.  This is a stronger
       result than that usually found in the literature, since the target of
       the homomorphism (operator ` O ` in our model) need not have any of the
       properties of a group as a prerequisite.  (Contributed by Paul Chapman,
       25-Apr-2008.) $)
    ghgrpi $p |- ( H e. GrpOp /\ ( G e. AbelOp -> H e. AbelOp ) ) $=
      ( wcel cfv co wceq va vb cgr cabl wi cgs cgi cgn eqid ghgrpilem4 wral w3a
      cv wa ablocom fveq2d ghgrpilem1 3adant1 ancoms 3eqtr3d 3coml 3expb opreq1
      opreq2 eqeq12d ghgrpilem2 anasss expl ralrimivv cdm cxp wss crn wfn ax-mp
      fndm resgrprn mp3an isablo biimpri sylancr pm3.2i ) FUCQZEUDQZFUDQZUEABCE
      UFRZEUGRZDEFEUHRZGHIJKLMNOPWGUIWHUIWFUIUJZWDWCUAUMZUBUMZFSZWKWJFSZTZUBIUK
      UAIUKZWEWIWDWNUAUBIIWDWJIQZWKIQWNWDWPUNWJBUMZDRZFSZWRWJFSZTZWNABBCWKDEFGH
      IJKLMNOPWQHQZWDWPXAXBWDUNAUMZDRZWRFSZWRXDFSZTZXAABACWJDEFGHIJKLMNOPXCHQZX
      BWDXGWDXHXBXGWDXHXBULZXCWQESZDRZWQXCESZDRZXEXFXIXJXLDXCWQEHKUOUPXHXBXKXET
      WDABCXCWQDEFGHIJKLMNOPUQURXHXBXMXFTZWDXBXHXNABCWQXCDEFGHIJKLMNOPUQUSURUTV
      AVBXDWJTXEWSXFWTXDWJWRFVCXDWJWRFVDVEVFVGWRWKTWSWLWTWMWRWKWJFVDWRWKWJFVCVE
      VFVHVIWEWCWOUNUAUBFIGVJCCVKZTZWCICVLIFVMTGXOVNXPNXOGVPVOWIMGFCIPVQVRVSVTW
      AWB $.
      $( [25-Apr-2008] $)
  $}

  ${
    $d F x y $.  $d H x y $.  $d O x y $.  $d S x y $.  $d W x y $.
    $d Z x y $.
    ghsubgi.1 $e |- S e. ( SubGrpOp ` G ) $.
    ghsubgi.2 $e |- X = ran G $.
    ghsubgi.3 $e |- F : X --> Y $.
    ghsubgi.4 $e |- Y C_ A $.
    ghsubgi.5 $e |- O Fn ( A X. A ) $.
    ghsubgi.6 $e |- ( ( x e. X /\ y e. X ) ->
                      ( F ` ( x G y ) ) = ( ( F ` x ) O ( F ` y ) ) ) $.
    ghsubgi.7 $e |- Z = ran S $.
    ghsubgi.8 $e |- W = ( F " Z ) $.
    ghsubgi.9 $e |- H = ( O |` ( W X. W ) ) $.
    $( The image of a subgroup ` S ` of group ` G ` under a group homomorphism
       ` F ` on ` G ` is a group, and furthermore is Abelian if ` S ` is
       Abelian.  (Contributed by Paul Chapman, 25-Apr-2008.) $)
    ghsubgi $p |- ( H e. GrpOp /\ ( S e. AbelOp -> H e. AbelOp ) ) $=
      ( cres cgr wcel wss csubg cfv w3a issubg mpbi simp2i wfo cima wfun cdm wf
      ffun ax-mp subgrnss fdmi sseqtr4i fores mp2an wceq foeq3 mpbir crn df-ima
      wb wa fssres frn eqsstri sstri cv co grpocl mp3an1 fvres wi subgopr sseli
      syl fveq2d syl2an 3eqtrd opreqan12d eqtr4d ghgrpi ) ABCELUBZDGHLIFUCUDZDU
      CUDZDFUEZDFUFUGUDZWKWLWMUHMFDUIUJUKZSLIWJULZLELUMZWJULZEUNZLEUOZUEWRJKEUP
      ZWSOJKEUQURLJWTWNLJUEZMFDLJNSUSURZJKEOUTVALEVBVCIWQVDWPWRVITIWQLWJVEURVFI
      KCIWQKTWQWJVGZKELVHXAXBXDKUEZOXCXAXBVJLKWJUPXEJKLEVKLKWJVLWCVCVMVMPVNQAVO
      ZLUDZBVOZLUDZVJZXFXHDVPZWJUGZXFEUGZXHEUGZHVPZXFWJUGZXHWJUGZHVPXJXLXKEUGZX
      FXHFVPZEUGZXOXJXKLUDZXLXRVDWLXGXIYAWOXFXHDLSVQVRXKLEVSWCXJXKXSEWNXJXKXSVD
      VTMXFXHFDLSWAURWDXGXFJUDXHJUDXTXOVDXILJXFXCWBLJXHXCWBRWEWFXGXIXPXMXQXNHXF
      LEVSXHLEVSWGWHUAWI $.
      $( [25-Apr-2008] $)
  $}


