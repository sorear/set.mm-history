$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Group homomorphism
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w z C $.  $d b c w x y z F $.  $d c w x y z G $.  $d a b c w x y z ph $.
    $d w ch $.  $d a b c x y z H $.  $d b w x y z X $.  $d a b c w x y z Y $.
    $d w D $.  $d w x y z O $.
    ghgrp.1 $e |- ( ph -> F : X -onto-> Y ) $.
    ${
      ghgrplem1.2 $e |- ( ( ph /\ w e. X ) -> ps ) $.
      ghgrplem1.3 $e |- ( C = ( F ` w ) -> ( ch <-> ps ) ) $.
      $( Lemma for ~ ghgrp . $)
      ghgrplem1 $p |- ( ( ph /\ C e. Y ) -> ch ) $=
        ( wcel cv cfv wceq wrex wfo foelrn sylan wa syl5ibrcom rexlimdva syldan
        imp ) AEHLZEDMZFNOZDGPZCAGHFQUEUHIDGHEFRSAUHCAUGCDGAUFGLTCUGBJKUAUBUDUC
        $.
        $( [12-May-2014] $) $( [25-Apr-2008] $)
    $}

    ghgrp.2 $e |- ( ( ph /\ ( x e. X /\ y e. X ) ) ->
                      ( F ` ( x G y ) ) = ( ( F ` x ) O ( F ` y ) ) ) $.
    ghgrp.3 $e |- H = ( O |` ( Y X. Y ) ) $.
    $( Lemma for ~ ghgrp . $)
    ghgrplem2 $p |- ( ( ph /\ ( C e. X /\ D e. X ) ) ->
                       ( F ` ( C G D ) ) = ( ( F ` C ) H ( F ` D ) ) ) $=
      ( vz vw wcel co cfv wceq wa wral ralrimivva opreq1 fveq2d opreq1d eqeq12d
      cv fveq2 opreq2 opreq2d cbvral2v sylib rcla42v syl5 impcom cres opreqi wf
      cxp wfo fof syl ffvelrn anim12dan sylan oprvres syl5eq eqtr4d ) ADJQZEJQZ
      UAZUAZDEGRZFSZDFSZEFSZIRZVPVQHRZVLAVOVRTZAOUHZPUHZGRZFSZWAFSZWBFSZIRZTZPJ
      UBOJUBZVLVTABUHZCUHZGRZFSZWJFSZWKFSZIRZTZCJUBBJUBWIAWQBCJJMUCWQWHWAWKGRZF
      SZWEWOIRZTBCOPJJWJWATZWMWSWPWTXAWLWRFWJWAWKGUDUEXAWNWEWOIWJWAFUIUFUGWKWBT
      ZWSWDWTWGXBWRWCFWKWBWAGUJUEXBWOWFWEIWKWBFUIUKUGULUMWHVTDWBGRZFSZVPWFIRZTO
      PDEJJWADTZWDXDWGXEXFWCXCFWADWBGUDUEXFWEVPWFIWADFUIUFUGWBETZXDVOXEVRXGXCVN
      FWBEDGUJUEXGWFVQVPIWBEFUIUKUGUNUOUPVMVSVPVQIKKUTUQZRZVRHXHVPVQNURVMVPKQZV
      QKQZUAZXIVRTAJKFUSZVLXLAJKFVAXMLJKFVBVCXMVJXJVKXKJKDFVDJKEFVDVEVFVPVQKKIV
      GVCVHVI $.
      $( [12-May-2014] $) $( [25-Apr-2008] $)

    ghgrp.4 $e |- X = ran G $.
    ghgrp.5 $e |- ( ph -> Y C_ A ) $.
    ghgrp.6 $e |- ( ph -> O Fn ( A X. A ) ) $.
    ${
      ghgrp.7 $e |- ( ph -> G e. GrpOp ) $.
      $( The image of a group ` G ` under a group homomorphism ` F ` is a
         group.  This is a stronger result than that usually found in the
         literature, since the target of the homomorphism (operator ` O ` in
         our model) need not have any of the properties of a group as a
         prerequisite.  (Contributed by Paul Chapman, 25-Apr-2008.)  (Revised
         by Mario Carneiro, 12-May-2014.) $)
      ghgrp $p |- ( ph -> H e. GrpOp ) $=
        ( wcel co wceq va vb vc vz cvv wfo crn cgr syl syl5eqel fornex sylc cgi
        rnexg cfv c0 wne wf fof eqid grpoidcl ffvelrn syl2anc ne0i cxp wfn wral
        cv wss xpss12 fnssres fneq1i sylibr adantr ghgrplem2 grpocl 3expb sylan
        cres wa syldan anassrs opreq2 eleq1d ghgrplem1 ralrimiva opreq1 ralbidv
        eqeltrrd ffnoprv sylanbrc simprll simprlr simprr grpoass syl13anc simpl
        wi fveq2d adantrr syl12anc syl3anc 3eqtr3d opreq1d opreq2d expr eqeq12d
        impancom imbi2d 3imp2 wrex cgs simprl grpodivcl grponpcan eqtr3d eqeq1d
        ex rcla4ev cgn grpoinvcl sylancom grpoasscan1 jca eqeq2 rexbidv anbi12d
        impr simpld simprd isgrp2d ) AUAUBUCGUEJAIUERIJEUFZJUERAIFUGZUENAFUHRZY
        MUERQFUHUNUIUJKIJUEEUKULAFUMUOZEUOZJRZJUPUQAIJEURZYOIRZYQAYLYRKIJEUSUIZ
        AYNYSQYOFINYOUTVAUIIJYOEVBVCJYPVDUIAGJJVEZVFZUAVHZUBVHZGSZJRZUBJVGZUAJV
        GUUAJGURAHUUAVSZUUAVFZUUBAHDDVEZVFUUAUUJVIZUUIPAJDVIZUULUUKOOJDJDVJVCUU
        JUUAHVKVCUUAGUUHMVLVMAUUGUAJABVHZEUOZUUDGSZJRZUBJVGUUGBUUCEIJKAUUMIRZVT
        ZUUPUBJUURUUNCVHZEUOZGSZJRZUUPCUUDEIJAYLUUQKVNAUUQUUSIRZUVBAUUQUVCVTZVT
        ZUUMUUSFSZEUOZUVAJABCUUMUUSEFGHIJKLMVOZAUVDUVFIRZUVGJRZAYNUVDUVIQYNUUQU
        VCUVIUUMUUSFINVPVQVRZAYRUVIUVJYTIJUVFEVBVRWAWIWBUUDUUTTZUUOUVAJUUDUUTUU
        NGWCZWDWEWFUUCUUNTZUUFUUPUBJUVNUUEUUOJUUCUUNUUDGWGZWDWHWEWFUAUBJJJGWJWK
        AUUCJRZUUDJRZUCVHZJRZUUEUVRGSZUUCUUDUVRGSZGSZTZAUVPUVQUVSUWCWRZWRZAUVQU
        VSUUOUVRGSZUUNUWAGSZTZWRZWRUWEBUUCEIJKAUVQUUQUWIAUUQUVSUVAUVRGSZUUNUUTU
        VRGSZGSZTZWRZWRUUQUWIWRCUUDEIJKAUUQUVCUWNAUUQUVCUWNAUVSUVDUWMAUVDUVAUDV
        HZEUOZGSZUUNUUTUWPGSZGSZTZWRUVDUWMWRUDUVREIJKAUVDUWOIRZUWTAUVDUXAUWTAUV
        DUXAVTZVTZUVGUWPGSZUUNUUSUWOFSZEUOZGSZUWQUWSUXCUVFUWOFSZEUOZUUMUXEFSZEU
        OZUXDUXGUXCUXHUXJEUXCYNUUQUVCUXAUXHUXJTAYNUXBQVNZAUUQUVCUXAWLZAUUQUVCUX
        AWMZAUVDUXAWNZUUMUUSUWOFINWOWPWSUXCAUVIUXAUXIUXDTAUXBWQZAUVDUVIUXAUVKWT
        UXOABCUVFUWOEFGHIJKLMVOXAUXCAUUQUXEIRZUXKUXGTUXPUXMUXCYNUVCUXAUXQUXLUXN
        UXOUUSUWOFINVPXBABCUUMUXEEFGHIJKLMVOXAXCUXCUVGUVAUWPGUXCAUUQUVCUVGUVATU
        XPUXMUXNUVHXAXDUXCUXFUWRUUNGUXCAUVCUXAUXFUWRTUXPUXNUXOABCUUSUWOEFGHIJKL
        MVOXAXEXCXFXHUVRUWPTZUWMUWTUVDUXRUWJUWQUWLUWSUVRUWPUVAGWCUXRUWKUWRUUNGU
        VRUWPUUTGWCXEXGXIWEXHXFXHUVLUWIUWNUUQUVLUWHUWMUVSUVLUWFUWJUWGUWLUVLUUOU
        VAUVRGUVMXDUVLUWAUWKUUNGUUDUUTUVRGWGXEXGXIXIWEXHUVNUWDUWIUVQUVNUWCUWHUV
        SUVNUVTUWFUWBUWGUVNUUEUUOUVRGUVOXDUUCUUNUWAGWGXGXIXIWEXRXJAUVPUVQVTVTZU
        VRUUCGSZUUDTZUCJXKZUUCUVRGSZUUDTZUCJXKZAUVPUVQUYBUYEVTZAUVQUVRUUNGSZUUD
        TZUCJXKZUUNUVRGSZUUDTZUCJXKZVTZWRUVQUYFWRBUUCEIJKAUVQUUQUYMAUUQUYGUUTTZ
        UCJXKZUYJUUTTZUCJXKZVTZWRUUQUYMWRCUUDEIJKAUUQUVCUYRAUUQUVCUYRUVEUYOUYQU
        VEUUSUUMFXLUOZSZEUOZJRZVUAUUNGSZUUTTZUYOUVEYRUYTIRZVUBAYRUVDYTVNZUVEYNU
        VCUUQVUEAYNUVDQVNZAUUQUVCWNZAUUQUVCXMZUUSUUMUYSFINUYSUTZXNXBZIJUYTEVBVC
        UVEUYTUUMFSZEUOZVUCUUTUVEAVUEUUQVUMVUCTAUVDWQZVUKVUIABCUYTUUMEFGHIJKLMV
        OXAUVEVULUUSEUVEYNUVCUUQVULUUSTVUGVUHVUIUUSUUMUYSFINVUJXOXBWSXPUYNVUDUC
        VUAJUVRVUATUYGVUCUUTUVRVUAUUNGWGXQXSVCUVEUUMFXTUOZUOZUUSFSZEUOZJRZUUNVU
        RGSZUUTTZUYQUVEYRVUQIRZVUSVUFUVEYNVUPIRZUVCVVBVUGUVEYNUUQVVCVUGVUIUUMFV
        UOINVUOUTZYAVCVUHVUPUUSFINVPXBZIJVUQEVBVCUVEUUMVUQFSZEUOZVUTUUTUVEAUUQV
        VBVVGVUTTVUNAUVDAUUQVUNVUIYBAUVDAVVBVUNVVEYBABCUUMVUQEFGHIJKLMVOXAUVEVV
        FUUSEUVEYNUUQUVCVVFUUSTVUGVUIVUHUUMUUSFVUOINVVDYCXBWSXPUYPVVAUCVURJUVRV
        URTUYJVUTUUTUVRVURUUNGWCXQXSVCYDXFXHUVLUYMUYRUUQUVLUYIUYOUYLUYQUVLUYHUY
        NUCJUUDUUTUYGYEYFUVLUYKUYPUCJUUDUUTUYJYEYFYGXIWEXHUVNUYFUYMUVQUVNUYBUYI
        UYEUYLUVNUYAUYHUCJUVNUXTUYGUUDUUCUUNUVRGWCXQYFUVNUYDUYKUCJUVNUYCUYJUUDU
        UCUUNUVRGWGXQYFYGXIWEYHZYIUXSUYBUYEVVHYJYK $.
        $( [12-May-2014] $) $( [25-Apr-2008] $)
    $}

    ghablo.7 $e |- ( ph -> G e. AbelOp ) $.
    $( The image of a Abelian group ` G ` under a group homomorphism ` F ` is
       an Abelian group (Contributed by Paul Chapman, 25-Apr-2008.)  (Revised
       by Mario Carneiro, 12-May-2014.) $)
    ghablo $p |- ( ph -> H e. AbelOp ) $=
      ( wcel co wceq va vb cgr crn wral cabl ablogrpo syl ghgrp cdm cxp wss wfn
      cv wa resgrprn syl3anc eleq2d anbi12d biimpar cfv wi adantr simprl simprr
      fndm ablocom fveq2d ghgrplem2 ancom2s expr opreq2 opreq1 imbi2d ghgrplem1
      3eqtr3d eqeq12d impancom impr syldan ralrimivva eqid isablo sylanbrc ) AG
      UCRZUAUNZUBUNZGSZWGWFGSZTZUBGUDZUEUAWKUEGUFRABCDEFGHIJKLMNOPAFUFRZFUCRQFU
      GUHUIZAWJUAUBWKWKAWFWKRZWGWKRZUOZWFJRZWGJRZUOZWJAWSWPAWQWNWRWOAJWKWFAHUJD
      DUKZTZWEJDULJWKTAHWTUMXAPWTHVFUHWMOHGDJMUPUQZURAJWKWGXBURUSUTAWQWRWJAWRBU
      NZEVAZWGGSZWGXDGSZTZVBWRWJVBBWFEIJKAWRXCIRZXGAXHXDCUNZEVAZGSZXJXDGSZTZVBX
      HXGVBCWGEIJKAXIIRZXHXMAXHXNXMAXHXNUOZUOZXCXIFSZEVAXIXCFSZEVAZXKXLXPXQXREX
      PWLXHXNXQXRTAWLXOQVCAXHXNVDAXHXNVEXCXIFINVGUQVHABCXCXIEFGHIJKLMVIAXNXHXSX
      LTABCXIXCEFGHIJKLMVIVJVPVJVKWGXJTZXGXMXHXTXEXKXFXLWGXJXDGVLWGXJXDGVMVQVNV
      OVRWFXDTZWJXGWRYAWHXEWIXFWFXDWGGVMWFXDWGGVLVQVNVOVSVTWAUAUBGWKWKWBWCWD $.
      $( [12-May-2014] $) $( [25-Apr-2008] $)
  $}

  ${
    $d x y F $.  $d x y H $.  $d x y O $.  $d x y S $.  $d x y W $.
    $d x y Z $.  $d x y ph $.
    ghsubg.1 $e |- ( ph -> S e. ( SubGrpOp ` G ) ) $.
    ghsubg.2 $e |- X = ran G $.
    ghsubg.3 $e |- ( ph -> F : X --> Y ) $.
    ghsubg.4 $e |- ( ph -> Y C_ A ) $.
    ghsubg.5 $e |- ( ph -> O Fn ( A X. A ) ) $.
    ghsubg.6 $e |- ( ( ph /\ ( x e. X /\ y e. X ) ) ->
                     ( F ` ( x G y ) ) = ( ( F ` x ) O ( F ` y ) ) ) $.
    ghsubg.7 $e |- Z = ran S $.
    ghsubg.8 $e |- W = ( F " Z ) $.
    ghsubg.9 $e |- H = ( O |` ( W X. W ) ) $.
    $( The image of a subgroup ` S ` of group ` G ` under a group homomorphism
       ` F ` on ` G ` is a group, and furthermore is Abelian if ` S ` is
       Abelian.  (Contributed by Paul Chapman, 25-Apr-2008.)  (Revised by Mario
       Carneiro, 12-May-2014.) $)
    ghsubglem $p |- ( ph -> ( H e. GrpOp /\
                      ( S e. AbelOp -> H e. AbelOp ) ) ) $=
      ( cgr wcel cabl wi cres cima wfun cdm wss wfo ffun syl csubg cfv subgrnss
      wf wceq fdm sseqtr4d fores syl2anc cv wa co ssel2 anim12dan syldan issubg
      sylan simp2bi grpocl 3expb fvres subgopr fveq2d opreqan12d adantl 3eqtr4d
      imp eqtrd cxp xpeq12i reseq2i eqtri crn imassrn syl5ss sstrd ghgrp adantr
      frn adantlr wfn simpr ghablo ex jca ) AHUCUDEUEUDZHUEUDZUFABCDFMUGZEHIMFM
      UHZAFUIZMFUJZUKMXCXBULZAKLFURZXDPKLFUMUNAMKXEAEGUOUPUDZMKUKZNGEMKOTUQUNZA
      XGXEKUSPKLFUTUNVAMFVBVCZABVDZMUDZCVDZMUDZVEZVEZXLXNGVFZFUPZXLFUPZXNFUPZIV
      FZXLXNEVFZXBUPZXLXBUPZXNXBUPZIVFZAXPXLKUDZXNKUDZVEZXSYBUSAXIXPYJXJXIXMYHX
      OYIMKXLVGMKXNVGVHVKSVIXQYDYCFUPZXSXQYCMUDZYDYKUSAEUCUDZXPYLAXHYMNXHGUCUDY
      MEGUKGEVJVLUNZYMXMXOYLXLXNEMTVMVNVKYCMFVOUNXQYCXRFAXPYCXRUSZAXHXPYOUFNXLX
      NGEMTVPUNWAVQWBXPYGYBUSAXMXOYEXTYFYAIXLMFVOXNMFVOVRVSVTZHIJJWCZUGIXCXCWCZ
      UGUBYQYRIJXCJXCUAUAWDWEWFZTAXCLDAXCFWGZLFMWHAXGYTLUKPKLFWMUNWIQWJZRYNWKAW
      TXAAWTVEBCDXBEHIMXCAXFWTXKWLAXPYDYGUSWTYPWNYSTAXCDUKWTUUAWLAIDDWCWOWTRWLA
      WTWPWQWRWS $.
      $( [12-May-2014] $)

    $( The image of a subgroup ` S ` of group ` G ` under a group homomorphism
       ` F ` on ` G ` is a group.  (Revised by Mario Carneiro, 12-May-2014.) $)
    ghsubg $p |- ( ph -> H e. GrpOp ) $=
      ( cgr wcel cabl wi ghsubglem simpld ) AHUCUDEUEUDHUEUDUFABCDEFGHIJKLMNOPQ
      RSTUAUBUGUH $.
      $( [12-May-2014] $) $( [25-Apr-2008] $)

    ghsubablo.10 $e |- ( ph -> S e. AbelOp ) $.
    $( The image of an Abelian subgroup ` S ` of group ` G ` under a group
       homomorphism ` F ` on ` G ` is an Abelian group.  (Revised by Mario
       Carneiro, 12-May-2014.) $)
    ghsubablo $p |- ( ph -> H e. AbelOp ) $=
      ( cabl wcel cgr wi ghsubglem simprd mpd ) AEUDUEZHUDUEZUCAHUFUEUKULUGABCD
      EFGHIJKLMNOPQRSTUAUBUHUIUJ $.
      $( [12-May-2014] $)
  $}


