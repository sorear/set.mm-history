$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Group homomorphism
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F x y $.  $d G a b x y $.  $d H a b x y $.  $d O x y $.  $d X x y $.
    $d Y a b x y $.

    ghgrpi.1 $e |- G e. Grp $.
    ghgrpi.2 $e |- X = ran G $.
    ghgrpi.3 $e |- F : X -onto-> Y $.
    ghgrpi.4 $e |- Y (_ A $.
    ghgrpi.5 $e |- O Fn ( A X. A ) $.
    ghgrpi.6 $e |- ( ( x e. X /\ y e. X ) ->
                     ( F ` ( x G y ) ) = ( ( F ` x ) O ( F ` y ) ) ) $.
    ghgrpi.7 $e |- H = ( O |` ( Y X. Y ) ) $.

    ${
      $d C a b $.  $d D b $.  $d F a b $.  $d O a b $.  $d X a b $.
      $( Lemma for ~ ghgrpi . $)
      ghgrpilem1 $p |- ( ( C e. X /\ D e. X ) ->
                         ( F ` ( C G D ) ) = ( ( F ` C ) H ( F ` D ) ) ) $=
        ( va vb wcel wa co cfv cv wceq wral rgen2a opreq1 fveq2d fveq2 opreq1d
        eqeq12d opreq2 opreq2d cbvral2v mpbi rcla42v mpi cxp cres oprvres
        opreqi syl5eq crn wfn wfo df-fo pm3.26i fnfvelrn mpan pm3.27i syl6eleq
        syl2an eqtr4d ) DJUAZEJUAZUBZDEGUCZFUDZDFUDZEFUDZIUCZWAWBHUCZVRSUEZTUEZ
        GUCZFUDZWEFUDZWFFUDZIUCZUFZTJUGSJUGZVTWCUFZAUEZBUEZGUCZFUDZWOFUDZWPFUDZ
        IUCZUFZBJUGAJUGWMXBABJQUHXBWLWEWPGUCZFUDZWIWTIUCZUFABSTJJWOWEUFZWRXDXAX
        EXFWQXCFWOWEWPGUIUJXFWSWIWTIWOWEFUKULUMWPWFUFZXDWHXEWKXGXCWGFWPWFWEGUNU
        JXGWTWJWIIWPWFFUKUOUMUPUQWLWNDWFGUCZFUDZWAWJIUCZUFSTDEJJWEDUFZWHXIWKXJX
        KWGXHFWEDWFGUIUJXKWIWAWJIWEDFUKULUMWFEUFZXIVTXJWCXLXHVSFWFEDGUNUJXLWJWB
        WAIWFEFUKUOUMURUSWAKUAZWBKUAZWDWCUFVPVQXMXNUBWAWBIKKUTVAZUCWCWDWAWBKKIV
        BHXOWAWBRVCVDVPWAFVEZKFJVFZVPWAXPUAXQXPKUFZJKFVGXQXRUBNJKFVHUQZVIZJDFVJ
        VKXQXRXSVLZVMVQWBXPKXQVQWBXPUAXTJEFVJVKYAVMVNVO $.
        $( [26-Apr-2008] $) $( [25-Apr-2008] $)
    $}

    ${
      $d C w $.  $d F w $.  $d X w $.  $d ch w $.  $d ph w $.
      ghgrpilem2.8 $e |- ( ( w e. X /\ ph ) -> ps ) $.
      ghgrpilem2.9 $e |- ( ( F ` w ) = C -> ( ps <-> ch ) ) $.
      $( Lemma for ~ ghgrpi . $)
      ghgrpilem2 $p |- ( ( ph /\ C e. Y ) -> ch ) $=
        ( wcel cv cfv wceq wrex wa ancoms syl5cbi r19.23adva crn wfn wfo df-fo
        mpbi pm3.27i eleq2i wb pm3.26i fvelrnb ax-mp bitr3i syl5ib imp ) AHNUDZ
        CAFUEZIUFHUGZFMUHZCVGAVICFMVIBCAVHMUDZUIUCVKABUBUJUKULVGHIUMZUDZVJVLNHI
        MUNZVLNUGZMNIUOVNVOUIQMNIUPUQZURUSVNVMVJUTVNVOVPVAFMHIVBVCVDVEVF $.
        $( [26-Apr-2008] $) $( [25-Apr-2008] $)
    $}

    ${
      $d D c $.  $d F c z $.  $d G c $.  $d H a b c x y z $.  $d N c $.
      $d X z $.  $d Y c z $.

      ghgrpilem3.8 $e |- U = ( Id ` G ) $.
      ghgrpilem3.9 $e |- N = ( inv ` G ) $.
      ghgrpilem3.10 $e |- D = ( /g ` G ) $.
      $( Lemma for ~ ghgrpi . $)
      ghgrpilem3 $p |- ( ( a e. Y /\ b e. Y ) ->
          ( E. c e. Y ( c H a ) = b /\ E. c e. Y ( a H c ) = b ) ) $=
        ( cv wcel wa co wceq wrex cfv opreq1 eqeq1d rcla4ev crn cgr grpdivcl
        mp3an1 wfn wfo df-fo mpbi pm3.26i fnfvelrn mpan syl pm3.27i syl6eleq
        ghgrpilem1 sylancom grpnpcan fveq2d eqtr3d sylanc ancoms opreq2
        rexbidv ghgrpilem2 eqeq2 grpcl grpinvcl sylan syldan grpasscan1 jca )
        MUFZLUGZNUFZLUGUHOUFZWGHUIZWIUJZOLUKZWGWJHUIZWIUJZOLUKZWHWKBUFZFULZUJZO
        LUKZWMABBCWIFGHJKLPQRSTUAUBWQKUGZWJAUFZFULZHUIZWRUJZOLUKZWTABACWGFGHJKL
        PQRSTUAUBXAXBKUGZXFWQXBDUIZFULZLUGXIXCHUIZWRUJZXFXAXGUHZXEXKOXILWJXIUJX
        DXJWRWJXIXCHUMUNUOXLXIFUPZLXLXHKUGZXIXMUGZGUQUGZXAXGXNPWQXBDGKQUEURUSZF
        KUTZXNXOXRXMLUJZKLFVAXRXSUHRKLFVBVCZVDZKXHFVEVFVGXRXSXTVHZVIXLXHXBGUIZF
        ULZXJWRXAXGXNYDXJUJXQABCXHXBFGHJKLPQRSTUAUBVJVKXLYCWQFXPXAXGYCWQUJPWQXB
        DGKQUEVLUSVMVNVOVPXCWGUJZXEWSOLYEXDWKWRXCWGWJHVQUNVRVSWRWIUJZWSWLOLWRWI
        WKVTVRVSWHWNWRUJZOLUKZWPABBCWIFGHJKLPQRSTUAUBXAXCWJHUIZWRUJZOLUKZYHABAC
        WGFGHJKLPQRSTUAUBXBIULZWQGUIZFULZLUGXCYNHUIZWRUJZYKXGXAUHZYJYPOYNLWJYNU
        JYIYOWRWJYNXCHVQUNUOYQYNXMLYQYMKUGZYNXMUGZYLKUGZXAYRXGXPYTXAYRPYLWQGKQW
        AUSXPXGYTPXBGIKQUDWBVFWCZXRYRYSYAKYMFVEVFVGYBVIYQXBYMGUIZFULZYOWRXGXAYR
        UUCYOUJUUAABCXBYMFGHJKLPQRSTUAUBVJWDYQUUBWQFXPXGXAUUBWQUJPXBWQGIKQUDWEU
        SVMVNVOYEYJYGOLYEYIWNWRXCWGWJHUMUNVRVSYFYGWOOLWRWIWNVTVRVSWF $.
        $( [26-Apr-2008] $) $( [25-Apr-2008] $)

      $( Lemma for ~ ghgrpi . $)
      ghgrpilem4 $p |- H e. Grp $=
        ( va vb vc vz cvv wcel wfo crn cgr rnexg ax-mp eqeltri fornex mp2 cfv
        c0 wne wfn wceq wa df-fo mpbi pm3.26i grpidcl fnfvelrn mp2an pm3.27i
        eleqtri ne0i cxp wf cv co wral ffnoprv cres wss ssxp fnssres wb fneq1
        mpbir ghgrpilem1 grpcl mp3an1 mpan syl eqeltrrd syl6eleq opreq1 eleq1d
        ghgrpilem2 opreq2 rgen2a mpbir2an w3a grpass fveq2d sylan 3impa sylan2
        3impb 3eqtr3d 3adant3 opreq1d 3adant1 opreq2d 3expb eqeq12d anasss
        wrex ghgrpilem3 pm3.26d pm3.27d isgrp2i ) UCUDUEHLKUGUHKLFUIZLUGUHKGUJZ
        UGNGUKUHZXSUGUHMGUKULUMUNOKLUGFUOUPEFUQZLUHLURUSYAFUJZLFKUTZEKUHZYAYBUH
        YCYBLVAZXRYCYEVBOKLFVCVDZVEZXTYDMEGKNTVFUMKEFVGVHYCYEYFVIZVJLYAVKUMLLVL
        ZLHVMHYIUTZUCVNZUDVNZHVOZLUHZUDLVPUCLVPUCUDLLLHVQYJJYIVRZYIUTZJCCVLZUTY
        IYQVSZYPQLCVSZYSYRPPLCLCVTVHYQYIJWAVHHYOVAYJYPWBSYIHYOWCUMWDYNUCUDLYKLU
        HZYKBVNZFUQZHVOZLUHZYNABBCYLFGHJKLMNOPQRSUUAKUHZAVNZFUQZUUBHVOZLUHUUDAB
        ACYKFGHJKLMNOPQRSUUFKUHZUUEVBZUUHYBLUUJUUFUUAGVOZFUQZUUHYBABCUUFUUAFGHJ
        KLMNOPQRSWEZUUJUUKKUHZUULYBUHZXTUUIUUEUUNMUUFUUAGKNWFWGZYCUUNUUOYGKUUKF
        VGWHWIWJYHWKUUGYKVAZUUHUUCLUUGYKUUBHWLZWMWNUUBYLVAZUUCYMLUUBYLYKHWOZWMW
        NWPWQYTYLLUHZUEVNZLUHYMUVBHVOZYKYLUVBHVOZHVOZVAZYTUVAVBZYMUFVNZFUQZHVOZ
        YKYLUVIHVOZHVOZVAZUVFABUFCUVBFGHJKLMNOPQRSUVHKUHZYTUVAUVMUVNYTVBUUCUVIH
        VOZYKUUBUVIHVOZHVOZVAZUVMABBCYLFGHJKLMNOPQRSUUEUVNYTUVRUUEUVNVBZUUHUVIH
        VOZUUGUVPHVOZVAZUVRABACYKFGHJKLMNOPQRSUUIUUEUVNUWBUUIUUEUVNWRZUULUVIHVO
        ZUUGUUAUVHGVOZFUQZHVOZUVTUWAUWCUUKUVHGVOZFUQZUUFUWEGVOZFUQZUWDUWGUWCUWH
        UWJFXTUWCUWHUWJVAMUUFUUAUVHGKNWSWHWTUUIUUEUVNUWIUWDVAZUUNUVNUWLUUJABCUU
        KUVHFGHJKLMNOPQRSWEUUPXAXBUUIUUEUVNUWKUWGVAZUUIUWEKUHZUWMUVSABCUUFUWEFG
        HJKLMNOPQRSWEXTUUEUVNUWNMUUAUVHGKNWFWGXCXDXEUWCUULUUHUVIHUUIUUEUULUUHVA
        UVNUUMXFXGUWCUWFUVPUUGHUUEUVNUWFUVPVAUUIABCUUAUVHFGHJKLMNOPQRSWEXHXIXEX
        JUUQUVTUVOUWAUVQUUQUUHUUCUVIHUURXGUUGYKUVPHWLXKWNXLUUSUVOUVJUVQUVLUUSUU
        CYMUVIHUUTXGUUSUVPUVKYKHUUBYLUVIHWLXIXKWNXLUVIUVBVAZUVJUVCUVLUVEUVIUVBY
        MHWOUWOUVKUVDYKHUVIUVBYLHWOXIXKWNXBUVGUVBYKHVOYLVAUELXMZYKUVBHVOYLVAUEL
        XMZABCDEFGHIJKLUCUDUEMNOPQRSTUAUBXNZXOUVGUWPUWQUWRXPXQ $.
        $( [26-Apr-2008] $) $( [25-Apr-2008] $)
    $}

    $( The image of a group ` G ` under a group homomorphism ` F ` is a group,
       and furthermore is Abelian if ` G ` is Abelian.  This is a stronger
       result than that usually found in the literature, since the target of
       the homomorphism (operator ` O ` in our model) need not have any of the
       properties of a group as a prerequisite.  (Contributed by Paul Chapman,
       25-Apr-2008.) $)
    ghgrpi $p |- ( H e. Grp /\ ( G e. Abel -> H e. Abel ) ) $=
      ( va vb cgr wcel cabl wi cgs cfv cgi cgn eqid ghgrpilem4 cv co wceq wral
      wa w3a ablcom fveq2d ghgrpilem1 3adant1 ancoms 3eqtr3d 3coml 3expb
      opreq1 opreq2 eqeq12d ghgrpilem2 anasss 3impa 3expib r19.21aivv cdm cxp
      wss crn wfn fndm ax-mp resgrprn mp3an isabl biimpri mpan syl pm3.2i ) FST
      ZEUATZFUATZUBABCEUCUDZEUEUDZDEFEUFUDZGHIJKLMNOPWIUGWJUGWHUGUHZWFQUIZRUIZF
      UJZWMWLFUJZUKZRIULQIULZWGWFWPQRIIWFWLITZWMITZWPWFWRWSWPWFWRUMWLBUIZDUDZFU
      JZXAWLFUJZUKZWPABBCWMDEFGHIJKLMNOPWTHTZWFWRXDXEWFUMAUIZDUDZXAFUJZXAXGFUJZ
      UKZXDABACWLDEFGHIJKLMNOPXFHTZXEWFXJWFXKXEXJWFXKXEUNZXFWTEUJZDUDZWTXFEUJZD
      UDZXHXIXLXMXODXFWTEHKUOUPXKXEXNXHUKWFABCXFWTDEFGHIJKLMNOPUQURXKXEXPXIUKZW
      FXEXKXQABCWTXFDEFGHIJKLMNOPUQUSURUTVAVBXGWLUKXHXBXIXCXGWLXAFVCXGWLXAFVDVE
      VFVGXAWMUKXBWNXCWOXAWMWLFVDXAWMWLFVCVEVFVHVIVJWEWQWGWKWGWEWQUMQRFIGVKCCVL
      ZUKZWEICVMIFVNUKGXRVOXSNXRGVPVQWKMGFCIPVRVSVTWAWBWCWD $.
      $( [26-Apr-2008] $) $( [25-Apr-2008] $)
  $}

  ${
    $d F x y $.  $d H x y $.  $d O x y $.  $d S x y $.  $d W x y $.
    $d Z x y $.
    ghsubgi.1 $e |- S e. ( SubGrp ` G ) $.
    ghsubgi.2 $e |- X = ran G $.
    ghsubgi.3 $e |- F : X --> Y $.
    ghsubgi.4 $e |- Y (_ A $.
    ghsubgi.5 $e |- O Fn ( A X. A ) $.
    ghsubgi.6 $e |- ( ( x e. X /\ y e. X ) ->
                      ( F ` ( x G y ) ) = ( ( F ` x ) O ( F ` y ) ) ) $.
    ghsubgi.7 $e |- Z = ran S $.
    ghsubgi.8 $e |- W = ( F " Z ) $.
    ghsubgi.9 $e |- H = ( O |` ( W X. W ) ) $.

    $( The image of a subgroup ` S ` of group ` G ` under a group homomorphism
       ` F ` on ` G ` is a group, and furthermore is Abelian if ` S ` is
       Abelian.  (Contributed by Paul Chapman, 25-Apr-2008.) $)
    ghsubgi $p |- ( H e. Grp /\ ( S e. Abel -> H e. Abel ) ) $=
      ( cres cgr wcel wss csubg cfv w3a issubg mpbi 3simp2i wfo cima wfun cdm
      wf ffun ax-mp subgrnss fdmi sseqtr4i fores mp2an wceq wb foeq3 mpbir crn
      df-ima wa fssres frn syl eqsstri sstri cv co grpcl mp3an1 fvres wi
      subgopr fveq2d sseli syl2an 3eqtrd opreqan12d eqtr4d ghgrpi ) ABCELUBZDGH
      LIFUCUDZDUCUDZDFUEZDFUFUGUDZWKWLWMUHMFDUIUJUKZSLIWJULZLELUMZWJULZEUNZLEUO
      ZUEWRJKEUPZWSOJKEUQURLJWTWNLJUEZMFDLJNSUSURZJKEOUTVALEVBVCIWQVDWPWRVETIWQ
      LWJVFURVGIKCIWQKTWQWJVHZKELVIXAXBXDKUEZOXCXAXBVJLKWJUPXEJKLEVKLKWJVLVMVCV
      NVNPVOQAVPZLUDZBVPZLUDZVJZXFXHDVQZWJUGZXFEUGZXHEUGZHVQZXFWJUGZXHWJUGZHVQX
      JXLXKEUGZXFXHFVQZEUGZXOXJXKLUDZXLXRVDWLXGXIYAWOXFXHDLSVRVSXKLEVTVMXJXKXSE
      WNXJXKXSVDWAMXFXHFDLSWBURWCXFJUDXHJUDXTXOVDXGXIRLJXFXCWDLJXHXCWDWEWFXGXIX
      PXMXQXNHXFLEVTXHLEVTWGWHUAWI $.
      $( [26-Apr-2008] $) $( [25-Apr-2008] $)
  $}


