$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c PosetRel $.
  $c supw $.
  $c infw $.
  $c LatRel $.

  $( Extend class notation with the class of all posets. $)
  cps $a class PosetRel $.

  $( Extend class notation with the supremum of an ordered set. $)
  cspw $a class supw $.

  $( Extend class notation with the infimum of an ordered set. $)
  cinf $a class infw $.

  $( Extend class notation with the class of all lattices. $)
  cla $a class LatRel $.

  $( Define the class of all posets (partially ordered sets) with weak ordering
     (e.g.  "less than or equal to" instead of "less than").  A poset is a
     relation which is transitive, reflexive, and antisymmetric. $)
  df-ps $a |- PosetRel = { r | ( Rel r /\ ( r o. r ) C_ r /\
              ( r i^i `' r ) = ( _I |` U. U. r ) ) } $.

  ${
    $d r x y z w $.
    $( Define suprema under weak orderings.  Unlike ~ df-sup for strong
       orderings, ` supw ` is evaluates to a member of the field of ` R ` iff
       the supremum exists.  Read ` R supw A ` as the ` R ` -supremum of set
       ` A ` . $)
    df-spw $a |- supw = ( r e. PosetRel , x e. _V |-> ( iota_ y e. U. U. r
     ( A. z e. x z r y /\ A. z e. U. U. r ( A. w e. x w r z -> y r z ) ) ) ) $.

    $( Define the class of all infima of a weak ordering relation. $)
    df-nfw $a |- infw = { <. <. r , x >. , y >. | y = ( `' r supw x ) } $.

    $( Define the class of all lattices, which are posets in which every two
       elements have upper and lower bounds. $)
    df-la $a |- LatRel = { r e. PosetRel | A. x e. dom r A. y e. dom r
        ( ( r supw { x , y } ) e. dom r /\ ( r infw { x , y } ) e. dom r ) } $.
  $}

  ${
    $d r x y R $.
    $( The predicate "is a poset" i.e. a transitive, reflexive, antisymmetric
       relation. $)
    isps $p |- ( R e. A -> ( R e. PosetRel <-> ( Rel R /\ ( R o. R ) C_ R /\
                ( R i^i `' R ) = ( _I |` U. U. R ) ) ) ) $=
      ( vr cv wrel ccom wss ccnv cin cid cuni cres wceq w3a cps releq coeq1
      coeq2 eqtrd id sseq12d cnveq ineq12d unieq unieqd reseq2d eqeq12d
      3anbi123d df-ps elab2g ) CDZEZUKUKFZUKGZUKUKHZIZJUKKZKZLZMZNBEZBBFZBGZBBH
      ZIZJBKZKZLZMZNCBOAUKBMZULVAUNVCUTVIUKBPVJUMVBUKBVJUMBUKFVBUKBUKQUKBBRSVJT
      ZUAVJUPVEUSVHVJUKBUOVDVKUKBUBUCVJURVGJVJUQVFUKBUDUEUFUGUHCUIUJ $.
      $( [11-May-2008] $)
  $}

  $( A poset is a relation. $)
  psrel $p |- ( A e. PosetRel -> Rel A ) $=
    ( cps wcel wrel ccom wss ccnv cin cid cuni cres wceq w3a isps ibi simp1
    syl ) ABCZADZAAEAFZAAGHIAJJKLZMZSRUBBANOSTUAPQ $.
    $( [12-May-2008] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    $( Lemma for ~ psref and others. $)
    pslem $p |- ( R e. PosetRel -> ( ( A e. S /\ B e. T /\ C e. U )
       -> ( ( ( A R B /\ B R C ) -> A R C ) /\ ( ( A e. U. U. R -> A R A ) /\ (
      ( A R B /\ B R A ) -> A = B ) ) ) ) ) $=
      ( vx vy vz cps wcel wrel ccom wss ccnv cin cid cuni cres wceq w3a wbr wa
      wi isps ibi cv wal wral cotr asymref2 wb breq12 3adant3 3adant1 anbi12d
      3adant2 imbi12d cla43gv anidms rcla4cv a1i ancoms eqeq12 cla42gv anim12d
      com12 syl2anb syl ) DKLZDMZDDNDOZDDPQRDSSZTUAZUBZAELZBFLZCGLZUBZABDUCZBCD
      UCZUDZACDUCZUEZAVNLAADUCZUEZWABADUCZUDZABUAZUEZUDZUDZUEZVKVPKDUFUGVMVOWNV
      LVMHUHZIUHZDUCZWPJUHZDUCZUDZWOWRDUCZUEZJUIIUIHUIZWOWODUCZHVNUJZWQWPWODUCZ
      UDZWOWPUAZUEZIUIHUIZUDZWNVOHIJDUKHIDULVTXCXKUDWMVTXCWEXKWLXBWEHIJABCEFGWO
      AUAZWPBUAZWRCUAZUBZWTWCXAWDXOWQWAWSWBXLXMWQWAUMXNWOAWPBDUNZUOXMXNWSWBUMXL
      WPBWRCDUNUPUQXLXNXAWDUMXMWOAWRCDUNURUSUTVQVRXKWLUEVSVQVRUDZXEWGXJWKXEWGUE
      XQXDWFHAVNXLXDWFUMWOAWOADUNVAVBVCXIWKHIABEFXLXMUDZXGWIXHWJXRWQWAXFWHXPXMX
      LXFWHUMWPBWOADUNVDUQWOAWPBVEUSVFVGUOVGVHVIUPVJ $.
      $( [12-May-2008] $)

    $( The domain and range of a poset equal its field. $)
    psdmrn $p |- ( R e. PosetRel -> ( dom R = U. U. R /\ ran R = U. U. R ) ) $=
      ( vx cps wcel cdm cuni wceq crn wss cun ssun1 dmrnssfld sstri a1i cv wbr
      wi w3a wa pslem simprl syl6 3expd pm2.43d visset breldm ssrdv eqssd
      ssun2 brelrn jca ) ACDZAEZAFFZGAHZUNGULUMUNUMUNIULUMUMUOJZUNUMUOKALZMNULB
      UNUMULBOZUNDZURURAPZURUMDULUSUTULUSUSUTQZULUSUSVAQULUSUSUSVAULUSUSUSRUTUT
      SZUTQZVAVBURURGQZSSVAURURURAUNUNUNTVCVAVDUAUBUCUDUDUDZURURABUEZUFUBUGUHUL
      UOUNUOUNIULUOUPUNUOUMUIUQMNULBUNUOULUSUTURUODVEURURAVFVFUJUBUGUHUK $.
      $( [13-May-2008] $)
  $}

  ${
    psref.1 $e |- X = dom R $.
    $( A poset is reflexive. $)
    psref $p |- ( ( R e. PosetRel /\ A e. X ) -> A R A ) $=
      ( cps wcel wbr cuni cdm wceq crn psdmrn simpld syl5eq eleq2d wi w3a wa
      pslem simprl syl6 3expd pm2.43d sylbid imp ) BEFZACFZAABGZUFUGABHHZFZUHUF
      CUIAUFCBIZUIDUFUKUIJBKUIJBLMNOUFUJUHUFUJUJUHPZUFUJUJULPUFUJUJUJULUFUJUJUJ
      QUHUHRZUHPZULUMAAJPZRRULAAABUIUIUISUNULUOTUAUBUCUCUCUDUE $.
      $( [13-May-2008] $)

    $( The range of a poset equals it domain. $)
    psrn $p |- ( R e. PosetRel -> X = ran R ) $=
      ( cps wcel cdm crn cuni wceq wa psdmrn eqtr3 syl syl5eq ) ADEZBAFZAGZCOPA
      HHZIQRIJPQIAKPQRLMN $.
      $( [7-Jul-2008] $)
  $}

  $( A poset is antisymmetric. $)
  psasym $p |- ( ( R e. PosetRel /\ A R B /\ B R A ) -> A = B ) $=
    ( cps wcel wbr w3a cvv wa wceq simp1 wrel psrel brrelex 3adant3 3adant2
    3jca syl3an1 3simpc wi cuni pslem simprr syl6 syl3c ) CDEZABCFZBACFZGUFAHEZ
    BHEZUJGZUGUHIZABJZUFUGUHKUFCLZUGUHUKCMUNUGUHGUIUJUJUNUGUIUHABCNOUNUHUJUGBAC
    NPZUOQRUFUGUHSUFUKUGBBCFIUGTZACUAUAEAACFTZULUMTZIIURABBCHHHUBUPUQURUCUDUE
    $.
    $( [12-May-2008] $)

  $( A poset is transitive. $)
  pstr $p |- ( ( R e. PosetRel /\ A R B /\ B R C ) -> A R C ) $=
    ( cps wcel wbr w3a cvv wa simp1 adantr wrel psrel brrelex 3adant3 3adant2
    simpr 3jca syl3anl1 3simpc wi cuni wceq pslem simpl syl6 syl3c wn cdm
    sylan breldmg sylancom eqid psref syldan wb brprc adantl mpbird pm2.61dan
    ) DEFZABDGZBCDGZHZCIFZACDGZVEVFJVBAIFZBIFZVFHZVCVDJZVGVEVBVFVBVCVDKLVBDMZVC
    VDVFVJDNZVLVCVDHZVFJVHVIVFVNVHVFVLVCVHVDABDOZPLVNVIVFVLVDVIVCBCDOQLVNVFRSTV
    EVKVFVBVCVDUALVBVJVKVGUBZADUCUCFAADGZUBVCBADGJABUDUBJZJVPABCDIIIUEVPVRUFUGU
    HVEVFUIZJVGVQVEVQVSVBVCVQVDVBVCADUJZFZVQVBVCVHWAVBVLVCVHVMVOUKABIDULUMADVTV
    TUNUOUPPLVSVGVQUQVEACDURUSUTVA $.
    $( [12-May-2008] $)

  ${
    $d r w x y z R $.  $d r w x y X $.  $d w x y z A $.
    spwval2.1 $e |- X = U. U. R $.
    $( Value of supremum under a weak ordering.  Read ` R supw A ` as "the
       ` R ` -supremum of ` A ` ." ` U. U. R ` is the field of a relation ` R `
       by ~ relfld .  Unlike ~ df-sup for strong orderings, the supremum exists
       iff ` R supw A ` belongs to the field. $)
    spwval2 $p |- ( ( R e. PosetRel /\ A e. V ) -> ( R supw A ) =
                    ( iota_ x e. X ( A. y e. A y R x /\
                      A. y e. X ( A. z e. A z R y -> x R y ) ) ) ) $=
      ( vr vw wcel cps cvv cspw co cv wbr wral wi wa crio wceq elisset cuni
      unieq unieqd syl6eqr breq ralbidv imbi12d raleqbidv anbi12d riotaeqbidv
      raleq imbi1d riotabidv df-spw riotaex ovmpt2 sylan2 ) DFKELKDMKEDNOBPZAPZ
      EQZBDRZCPZVAEQZCDRZVBVAEQZSZBGRZTZAGUAZUBDFUCIJEDLMVAVBIPZQZBJPZRZVEVAVMQ
      ZCVORZVBVAVMQZSZBVMUDZUDZRZTZAWBUAVLNVCBVORZVFCVORZVHSZBGRZTZAGUAVMEUBZWD
      WIAWBGWJWBEUDZUDGWJWAWKVMEUEUFHUGZWJVPWEWCWHWJVNVCBVOVAVBVMEUHUIWJVTWGBWB
      GWLWJVRWFVSVHWJVQVFCVOVEVAVMEUHUIVBVAVMEUHUJUKULUMVODUBZWIVKAGWMWEVDWHVJV
      CBVODUNWMWGVIBGWMWFVGVHVFCVODUNUOUIULUPJABCIUQVKAGURUSUT $.
      $( [20-Nov-2013] $) $( [13-May-2008] $)
  $}

  ${
    $d r w x y z R $.  $d r w x y X $.  $d w x y z A $.
    spwval.1 $e |- X = dom R $.
    $( Value of supremum under a weak ordering.  Read ` R supw A ` as "the
       ` R ` -supremum of ` A ` ." ` U. U. R ` is the field of a relation ` R `
       by ~ relfld .  Unlike ~ df-sup for strong orderings, the supremum exists
       iff ` R supw A ` belongs to the field. $)
    spwval $p |- ( ( R e. PosetRel /\ A e. V ) -> ( R supw A ) =
                    ( iota_ x e. X ( A. y e. A y R x /\
                      A. y e. X ( A. z e. A z R y -> x R y ) ) ) ) $=
      ( cps wcel wa cspw co cv wbr wral wi cuni crio eqid spwval2 wceq cdm crn
      psdmrn simpld syl5eq raleqdv anbi2d riotaeqbidv adantr eqtr4d ) EIJZDFJZK
      EDLMBNZANZEOBDPZCNUOEOCDPUPUOEOQZBERRZPZKZAUSSZUQURBGPZKZAGSZABCDEFUSUSTU
      AUMVEVBUBUNUMVDVAAGUSUMGEUCZUSHUMVFUSUBEUDUSUBEUEUFUGZUMVCUTUQUMURBGUSVGU
      HUIUJUKUL $.
      $( [20-Nov-2013] $) $( [13-May-2008] $)
  $}

  ${
    $d w x y z A $.  $d y z B $.  $d y z C $.  $d w x y z R $.  $d w x y X $.
    $d y U $.  $d y W $.
    spwmo.1 $e |- ( ph
         <-> ( A. y e. A y R x /\ A. y e. X ( A. z e. A z R y -> x R y ) ) ) $.
    $( A poset has at most one supremum. $)
    spwmo $p |- ( R e. PosetRel -> E* x ( x e. X /\ ph ) ) $=
      ( vw cps wcel cv wbr wral wi wa wceq wal wmo breq1 cbvralv breq2 ralbidv
      imbi12d rcla4v imp3a im2anan9r psasym 3expib syl9 com13 exp43 com4r
      sylbi imp43 com3r imp an4s com12 19.21aivv anbi2i mobii eleq1 syl6bb
      imbi2d anbi12d mo4 bitri sylibr ) FJKZBLZGKZCLZVKFMZCENZDLZVMFMZDENZVKVMF
      MZOZCGNZPZPZILZGKZVPWDFMZDENZVRWDVMFMZOZCGNZPZPZPZVKWDQZOZIRBRZVLAPZBSZVJ
      WOBIWMVJWNVLWEWBWKVJWNOZVLWEPZWBWKPZWSXAVJWTWNVOWAWGWJVJWTWNOOZVOVPVKFMZD
      ENZWAWGWJXBOOOVNXCCDEVMVPVKFTUAWAWGWJXDXBWAWGWJXDXBWTVJWAWGPZWJXDPZPZWNWT
      XGVKWDFMZWDVKFMZPVJWNWEXEXHVLXFXIWEWAWGXHVTWGXHOCWDGVMWDQZVRWGVSXHXJVQWFD
      EVMWDVPFUBUCVMWDVKFUBUDUEUFVLWJXDXIWIXDXIOCVKGVMVKQZVRXDWHXIXKVQXCDEVMVKV
      PFUBUCVMVKWDFUBUDUEUFUGVJXHXIWNVKWDFUHUIUJUKULUMUNUOUPUQURUSUTWRWCBSWPWQW
      CBAWBVLHVAVBWCWLBIWNVLWEWBWKVKWDGVCWNVOWGWAWJWNVOVMWDFMZCENWGWNVNXLCEVKWD
      VMFUBUCXLWFCDEVMVPWDFTUAVDWNVTWICGWNVSWHVRVKWDVMFTVEUCVFVFVGVHVI $.
      $( [13-May-2008] $)

    $( A supremum is unique. $)
    spweu $p |- ( ( R e. PosetRel /\ E. x e. X ph ) -> E! x e. X ph ) $=
      ( wrex cps wcel wreu wa cv wmo spwmo anim2i reu5 sylibr ancoms ) ABGIZFJK
      ZABGLZUAUBMUABNGKAMBOZMUCUBUDUAABCDEFGHPQABGRST $.
      $( [15-May-2008] $)

    $( Property of supremum defining condition for an unordered pair. $)
    spwpr2 $p |- ( ( ( R e. T /\ A = { B , C } ) /\ ( B e. U /\ C e. W ) )
       -> ( ph <->
     ( ( B R x /\ C R x ) /\ A. y e. X ( ( B R y /\ C R y ) -> x R y ) ) ) ) $=
      ( cv wbr wral wi wa wcel cpr wceq wb raleq wal df-ral wo visset elpr
      imbi1i jaob bitri albii 19.26 3bitri ax-17 breq1 ceqsalg bi2anan9 syl5bb
      sylan9bb imbi1d ralbidv anbi12d adantll ) ACNZBNZHOZCEPZDNZVEHOZDEPZVFVEH
      OZQZCLPZRZHISZEFGTZUAZRFJSZGKSZRZRFVFHOZGVFHOZRZFVEHOZGVEHOZRZVLQZCLPZRZM
      VRWAVOWJUBVPVRWARZVHWDVNWIVRVHVGCVQPZWAWDVGCEVQUCWLVEFUAZVGQZCUDZVEGUAZVG
      QZCUDZRZWAWDWLVEVQSZVGQZCUDWNWQRZCUDWSVGCVQUEXAXBCXAWMWPUFZVGQXBWTXCVGVEF
      GCUGUHUIWMVGWPUJUKULWNWQCUMUNVSWOWBVTWRWCVGWBCFJWBCUOVEFVFHUPUQVGWCCGKWCC
      UOVEGVFHUPUQURUSUTWKVMWHCLWKVKWGVLVRVKVJDVQPZWAWGVJDEVQUCXDVIFUAZVJQZDUDZ
      VIGUAZVJQZDUDZRZWAWGXDVIVQSZVJQZDUDXFXIRZDUDXKVJDVQUEXMXNDXMXEXHUFZVJQXNX
      LXOVJVIFGDUGUHUIXEVJXHUJUKULXFXIDUMUNVSXGWEVTXJWFVJWEDFJWEDUOVIFVEHUPUQVJ
      WFDGKWFDUOVIGVEHUPUQURUSUTVAVBVCVDUS $.
      $( [24-Jun-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z R $.  $d x y X $.
    spwex.1 $e |- X = dom R $.
    spwex.2 $e |- ( ph <->
         ( A. y e. A y R x /\ A. y e. X ( A. z e. A z R y -> x R y ) ) ) $.
    $( A supremum exists iff ` R supw A ` belongs to the domain of ` R ` . $)
    spwex $p |- ( ( R e. PosetRel /\ A e. V )
               -> ( E. x e. X ph <-> ( R supw A ) e. X ) ) $=
      ( cps wcel wa wreu cv wbr wral wi crio wrex cspw co wb reubii cvv cdm
      dmexg syl5eqel riotaclbg syl syl5bb adantr spweu ex reurex impbid1
      spwval eleq1d 3bitr4d ) FKLZEGLZMZABHNZCOZBOZFPCEQDOVDFPDEQVEVDFPRCHQMZBH
      SZHLZABHTZFEUAUBZHLUTVCVHUCVAVCVFBHNZUTVHAVFBHJUDUTHUELVKVHUCUTHFUFUEIFKU
      GUHVFBHUEUIUJUKULUTVIVCUCVAUTVIVCUTVIVCABCDEFHJUMUNABHUOUPULVBVJVGHBCDEFG
      HIUQURUS $.
      $( [20-Nov-2013] $) $( [15-May-2008] $)

    $( Closure of a supremum. $)
    spwcl $p |- ( ( R e. PosetRel /\ A e. V /\ E. x e. X ph )
           -> ( R supw A ) e. X ) $=
      ( cps wcel wrex cspw co wa spwex biimpa 3impa ) FKLZEGLZABHMZFENOHLZTUAPU
      BUCABCDEFGHIJQRS $.
      $( [20-Nov-2013] $) $( [15-May-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.  $d x y z X $.
    spwpr4.1 $e |- X = dom R $.
    $( Supremum of an unordered pair. $)
    spwpr4 $p |- ( ( ( R e. PosetRel /\ C e. D ) /\ ( A R C /\ B R C ) /\
   A. x e. X ( ( A R x /\ B R x ) -> C R x ) ) -> ( R supw { A , B } ) = C ) $=
      ( vy vz cps wcel wa wbr cv wi wral cpr cspw co wceq simpll crn wrel
      psrel relelrng syl3an2 3com12 3expa psrn ad2antrr eleqtrrd adantrr jca
      3adant3 w3a crio cvv simp1l prex spwval sylancl wb brrelex ex anim12d
      syl imp adantlr eqid biid spwpr2 mpanl2 syl11anc riotabidv 3simpc wreu
      simp1r wrex breq2 anbi12d breq1 imbi2d ralbidv rcla4ev 3impb 3adant1l
      rexbidv mpbird spweu reubidv mpbid ax-17 a17d riota2f 3eqtrd syld3an1 )
      FKLZDGLZMZBDFNZCDFNZMZWRDELZMZBAOZFNCXFFNMZDXFFNZPZAGQZFBCRZSTZDUAXEXCWTX
      JXEXCMWRWSWRXDXCUBXEXAWSXBXEXAMDFUCZGWRXDXADXMLZXDWRXAXNWRXDFUDZXAXNFUEZB
      DEFUFUGUHUIWRGXMUAXDXAFGHUJUKULUMUNUOWTXCXJUPZXLXFIOZFNAXKQJOZXFFNJXKQXRX
      FFNZPAGQMZIGUQZBXRFNZCXRFNZMZXGXTPZAGQZMZIGUQZDXQWRXKURLXLYBUAWRWSXCXJUSZ
      BCUTIAJXKFURGHVAVBWTXCYBYIUAXJWTXCMZYAYHIGYKWRBURLZCURLZMZYAYHVCZWRWSXCUB
      WRXCYNWSWRXCYNWRXOXCYNPXPXOXAYLXBYMXOXAYLBDFVDVEXOXBYMCDFVDVEVFVGVHVIWRXK
      XKUAYNYOXKVJYAIAJXKBCFKURURGYAVKZVLVMVNZVOUOXQXCXJMZYIDUAZWTXCXJVPXQWSYHI
      GVQZYRYSVCWRWSXCXJVRXQYAIGVQZYTXQWRYAIGVSZUUAYJXQUUBYHIGVSZWSXCXJUUCWRWSX
      CXJUUCYHYRIDGXRDUAZYEXCYGXJUUDYCXAYDXBXRDBFVTXRDCFVTWAUUDYFXIAGUUDXTXHXGX
      RDXFFWBWCWDWAZWEWFWGWTXCUUBUUCVCXJYKYAYHIGYQWHUOWIYAIAJXKFGYPWJVNWTXCUUAY
      TVCXJYKYAYHIGYQWKUOWLYHYRIJGDXSDLIWMWSYRIWNUUEWOVNWLWPWQ $.
      $( [20-Nov-2013] $) $( [7-Jul-2008] $)

$(
    @{
      spwpr5.2 @e |- C = ( R supw { A , B } ) @.
      @( Supremum of an unordered pair. @)
      spwpr5 @p |- ( ( ( R e. PosetRel /\ C e. X ) /\ ( A R C /\ B R C ) /\
   A. x e. X ( ( A R x /\ B R x ) -> C R x ) ) -> ( R supw { A , B } ) = C ) @=
        (  ) ? @.
        @( [ ?] @) @( [19-Jul-2008] @)
    @}
$)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.
    $( Supremum of an unordered pair of comparable elements. $)
    spwpr4c $p |- ( ( R e. PosetRel /\ B e. U /\ A R B )
         -> ( R supw { A , B } ) = B ) $=
      ( vx cps wcel wbr w3a wa cv wi cdm wral cpr cspw co wceq 3simpa simp3
      simp1 crn cvv wrel psrel brrelex sylan 3adant2 brelrng syld3an1 eqid
      psrn 3ad2ant1 eleqtrrd psref syl11anc simpr a1i rgen spwpr4 syl121anc )
      CFGZBDGZABCHZIZVBVCJVDBBCHZAEKZCHZBVGCHZJVILZECMZNZCABOPQBRVBVCVDSVBVCVDT
      VEVBBVKGVFVBVCVDUAVEBCUBZVKAUCGZVCVBVDBVMGVBVDVNVCVBCUDVDVNCUEABCUFUGUHAB
      CUCDUIUJVBVCVKVMRVDCVKVKUKZULUMUNBCVKVOUOUPVLVEVJEVKVJVGVKGVHVIUQURUSUREA
      BBDCVKVOUTVA $.
      $( [9-Jun-2009] $) $( [7-Jul-2008] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d r x y R $.  $d r x y X $.
    isla.1 $e |- X = dom R $.
    $( The predicate "is a lattice" i.e. a poset in which any two elements have
       upper and lower bounds. $)
    isla $p |- ( R e. LatRel <-> ( R e. PosetRel /\ A. x e. X A. y e. X
      ( ( R supw { x , y } ) e. X /\ ( R infw { x , y } ) e. X ) ) ) $=
      ( vr cv cpr cspw co cdm wcel cinf wa wral cps cla wceq dmeq syl6eqr
      opreq1 eleq12d anbi12d raleqbidv df-la elrab2 ) FGZAGBGHZIJZUGKZLZUGUHMJZ
      UJLZNZBUJOZAUJOCUHIJZDLZCUHMJZDLZNZBDOZADOFCPQUGCRZUOVAAUJDVBUJCKDUGCSETZ
      VBUNUTBUJDVCVBUKUQUMUSVBUIUPUJDUGCUHIUAVCUBVBULURUJDUGCUHMUAVCUBUCUDUDABF
      UEUF $.
      $( [10-Jun-2009] $) $( [12-Jun-2008] $)

    $( Closure of the supremum (join) of two lattice elements. $)
    laspwcl $p |- ( ( R e. LatRel /\ A e. X /\ B e. X )
                -> ( R supw { A , B } ) e. X ) $=
      ( vx vy cla wcel cpr cspw co cv wral wa cps cinf isla simpl ralimi
      adantl sylbi wceq preq1 opreq2d eleq1d preq2 rcla42v mpan9 3impb ) CHIZAD
      IZBDIZCABJZKLZDIZUKCFMZGMZJZKLZDIZGDNZFDNZULUMOUPUKCPIZVACUSQLDIZOZGDNZFD
      NZOVCFGCDERVHVCVDVGVBFDVFVAGDVAVESTTUAUBVAUPCAURJZKLZDIFGABDDUQAUCZUTVJDV
      KUSVICKUQAURUDUEUFURBUCZVJUODVLVIUNCKURBAUGUEUFUHUIUJ $.
      $( [1-Jul-2009] $) $( [12-Jun-2008] $)

    $( Closure of the infimum (meet) of two lattice elements. $)
    lanfwcl $p |- ( ( R e. LatRel /\ A e. X /\ B e. X )
                -> ( R infw { A , B } ) e. X ) $=
      ( vx vy cla wcel cpr cinf co cv wral wa cps cspw isla simpr ralimi
      adantl sylbi wceq preq1 opreq2d eleq1d preq2 rcla42v mpan9 3impb ) CHIZAD
      IZBDIZCABJZKLZDIZUKCFMZGMZJZKLZDIZGDNZFDNZULUMOUPUKCPIZCUSQLDIZVAOZGDNZFD
      NZOVCFGCDERVHVCVDVGVBFDVFVAGDVEVASTTUAUBVAUPCAURJZKLZDIFGABDDUQAUCZUTVJDV
      KUSVICKUQAURUDUEUFURBUCZVJUODVLVIUNCKURBAUGUEUFUHUIUJ $.
      $( [15-Jul-2009] $) $( [20-Jun-2008] $)
  $}

  ${
    $d x y R $.
    $( A lattice is a poset. $)
    laps $p |- ( R e. LatRel -> R e. PosetRel ) $=
      ( vx vy cla wcel cps cv cpr cspw co cdm cinf wa wral eqid isla simplbi )
      ADEAFEABGCGHZIJAKZEARLJSEMCSNBSNBCASSOPQ $.
      $( [28-Jul-2011] $) $( [12-Jun-2008] $)
  $}

