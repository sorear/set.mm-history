$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of subspaces of a Hilbert space.  See ~ sh for its
     membership relation.  Basically, a subspace is a subset of a Hilbert space
     that acts like a vector space.  From Definition of [Beran] p. 95. $)
  df-sh $a |- SH = { h e. ~P ~H | ( 0h e. h /\
              ( +h " ( h X. h ) ) C_ h /\ ( .h " ( CC X. h ) ) C_ h ) } $.

  ${
    $d h x y H $.
    $( Subspace ` H ` of a Hilbert space.  A subspace is a subset of Hilbert
       space which contains the zero vector and is closed under vector addition
       and scalar multiplication.  (Contributed by Mario Carneiro,
       23-Dec-2013.) $)
    issh $p |- ( H e. SH <-> ( ( H C_ ~H /\ 0h e. H ) /\
               ( ( +h " ( H X. H ) ) C_ H /\ ( .h " ( CC X. H ) ) C_ H ) ) ) $=
      ( vh chil cpw wcel c0v cva cxp cima wss csm cc w3a wa csh ax-hilex elpw2
      3anass anbi12i cv wceq eleq2 id xpeq12d imaeq2d sseq12d xpeq2 3anbi123d
      df-sh elrab2 anass 3bitr4i ) ACDZEZFAEZGAAHZIZAJZKLAHZIZAJZMZNACJZUOURVAN
      ZNZNAOEVCUONVDNUNVCVBVEACPQUOURVARSFBTZEZGVFVFHZIZVFJZKLVFHZIZVFJZMVBBAUM
      OVFAUAZVGUOVJURVMVAVFAFUBVNVIUQVFAVNVHUPGVNVFAVFAVNUCZVOUDUEVOUFVNVLUTVFA
      VNVKUSKVFALUGUEVOUFUHBUIUJVCUOVDUKUL $.
      $( [23-Dec-2013] $)

    $( Subspace ` H ` of a Hilbert space.  A subspace is a subset of Hilbert
       space which contains the zero vector and is closed under vector addition
       and scalar multiplication.  Definition of [Beran] p. 95.  (Revised by
       Mario Carneiro, 23-Dec-2013.) $)
    issh2 $p |- ( H e. SH <-> ( ( H C_ ~H /\ 0h e. H ) /\
                  ( A. x e. H A. y e. H ( x +h y ) e. H /\
                    A. x e. CC A. y e. H ( x .h y ) e. H ) ) ) $=
      ( csh wcel chil wss c0v wa cva cxp cima csm cc cv co wral issh wb wfun
      cdm wf ax-hfvadd ffun ax-mp xpss12 anidms fdmi syl6sseqr funimass4b
      sylancr ax-hfvmul xpss2 anbi12d adantr pm5.32i bitri ) CDECFGZHCEZIZJCCKZ
      LCGZMNCKZLCGZIZIUTAOZBOZJPCEBCQACQZVFVGMPCEBCQANQZIZICRUTVEVJURVEVJSUSURV
      BVHVDVIURJTZVAJUAZGVBVHSFFKZFJUBVKUCVMFJUDUEURVAVMVLURVAVMGCFCFUFUGVMFJUC
      UHUIABCCCJUJUKURMTZVCMUAZGVDVISNFKZFMUBVNULVPFMUDUEURVCVPVOCFNUMVPFMULUHU
      IABNCCMUJUKUNUOUPUQ $.
      $( [23-Dec-2013] $) $( [16-Aug-1999] $)
  $}

  $( A subspace is a subset of Hilbert space. $)
  shss $p |- ( H e. SH -> H C_ ~H ) $=
    ( csh wcel chil wss c0v wa cva cxp cima csm cc issh simplbi simpld ) ABCZAD
    EZFACZPQRGHAAIJAEKLAIJAEGAMNO $.
    $( [23-Dec-2013] $) $( [9-Oct-1999] $)

  $( A member of a subspace of a Hilbert space is a vector. $)
  shel $p |- ( ( H e. SH /\ A e. H ) -> A e. ~H ) $=
    ( csh wcel chil shss sseld imp ) BCDZABDAEDIBEABFGH $.
    $( [14-Dec-2004] $)

  $( The set of subspaces of a Hilbert space exists (is a set). $)
  shex $p |- SH e. _V $=
    ( vx csh chil cpw ax-hilex pwex cv wcel wss shss visset elpw sylibr ssriv
    ssexi ) BCDZCEFABPAGZBHQCIQPHQJQCAKLMNO $.
    $( [23-Oct-1999] $)

  ${
    shssi.1 $e |- H e. SH $.
    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    shssii $p |- H C_ ~H $=
      ( csh wcel chil wss shss ax-mp ) ACDAEFBAGH $.
      $( [6-Oct-1999] $)

    $( A member of a subspace of a Hilbert space is a vector. $)
    sheli $p |- ( A e. H -> A e. ~H ) $=
      ( chil shssii sseli ) BDABCEF $.
      $( [6-Oct-1999] $)

    ${
      sheli.1 $e |- A e. H $.
      $( A member of a subspace of a Hilbert space is a vector. $)
      shelii $p |- A e. ~H $=
        ( chil shssii sselii ) BEABCFDG $.
        $( [6-Oct-1999] $)
    $}
  $}

  $( The zero vector belongs to any subspace of a Hilbert space. $)
  sh0 $p |- ( H e. SH -> 0h e. H ) $=
    ( csh wcel chil wss c0v wa cva cxp cima csm cc issh simplbi simprd ) ABCZAD
    EZFACZPQRGHAAIJAEKLAIJAEGAMNO $.
    $( [23-Dec-2013] $) $( [11-Oct-1999] $)

  ${
    $d x y A $.  $d x y H $.  $d y B $.
    $( Closure of vector addition in a subspace of a Hilbert space. $)
    shaddcl $p |- ( ( H e. SH /\ A e. H /\ B e. H ) -> ( A +h B ) e. H ) $=
      ( vx vy csh wcel cva co cv wral wa csm cc chil wss c0v issh2 simprbi
      simpld wceq opreq1 eleq1d opreq2 rcla42v syl5com 3impib ) CFGZACGZBCGZABH
      IZCGZUHDJZEJZHIZCGZECKDCKZUIUJLULUHUQUMUNMICGECKDNKZUHCOPQCGLUQURLDECRSTU
      PULAUNHIZCGDEABCCUMAUAUOUSCUMAUNHUBUCUNBUAUSUKCUNBAHUDUCUEUFUG $.
      $( [21-May-2007] $) $( [13-Sep-1999] $)

    $( Closure of vector scalar multiplication in a subspace of a Hilbert
       space. $)
    shmulcl $p |- ( ( H e. SH /\ A e. CC /\ B e. H ) -> ( A .h B ) e. H ) $=
      ( vx vy csh wcel cc csm co cv wral wa cva chil wss c0v issh2 simprbi
      simprd wceq opreq1 eleq1d opreq2 rcla42v syl5com 3impib ) CFGZAHGZBCGZABI
      JZCGZUHDKZEKZIJZCGZECLDHLZUIUJMULUHUMUNNJCGECLDCLZUQUHCOPQCGMURUQMDECRSTU
      PULAUNIJZCGDEABHCUMAUAUOUSCUMAUNIUBUCUNBUAUSUKCUNBAIUDUCUEUFUG $.
      $( [21-Feb-2007] $) $( [13-Sep-1999] $)

    $( Closure of vector scalar multiplication in a subspace of a Hilbert
       space. $)
    shmulclOLD $p |- ( H e. SH ->
                   ( ( A e. CC /\ B e. H ) -> ( A .h B ) e. H ) ) $=
      ( vx vy csh wcel cv csm co wral cc wa cva chil wss c0v issh2 simprbi
      simprd wceq opreq1 eleq1d opreq2 rcla42v syl5com ) CFGZDHZEHZIJZCGZECKDLK
      ZALGBCGMABIJZCGZUGUHUINJCGECKDCKZULUGCOPQCGMUOULMDECRSTUKUNAUIIJZCGDEABLC
      UHAUAUJUPCUHAUIIUBUCUIBUAUPUMCUIBAIUDUCUEUF $.
      $( [13-Sep-1999] $)

    $(
    @( Reversal of subtraction remains a member of a subspace of a Hilbert
       space. @)
    shsubrclt @p  |- ( H e. SH -> ( ( A e. ~H /\ B e. ~H ) ->
                    ( ( A -h B ) e. H <-> ( B -h A ) e. H ) ) ) @=
?@.
    $)

    $( Subspace ` H ` of a Hilbert space. $)
    issh3 $p |- ( H C_ ~H -> ( H e. SH <-> ( 0h e. H /\
                  ( A. x e. H A. y e. H ( x +h y ) e. H /\
                    A. x e. CC A. y e. H ( x .h y ) e. H ) ) ) ) $=
      ( csh wcel chil wss c0v wa cv cva co wral csm cc issh2 anass baib syl5bb
      ) CDECFGZHCEZIAJZBJZKLCEBCMACMUBUCNLCEBCMAOMIZIZTUAUDIZABCPUETUFTUAUDQRS
      $.
      $( [16-Aug-1999] $)
  $}

  $( Closure of vector subtraction in a subspace of a Hilbert space. $)
  shsubcl $p |- ( ( H e. SH /\ A e. H /\ B e. H ) -> ( A -h B ) e. H ) $=
    ( csh wcel w3a cmv co c1 cneg csm cva chil wa wceq shss sseld anim12d
    3impib hvsubval syl cc ax-1cn negcli shmulcl mp3an2 3adant2 shaddcl
    syld3an3 eqeltrd ) CDEZACEZBCEZFZABGHZAIJZBKHZLHZCUNAMEZBMEZNZUOUROUKULUMVA
    UKULUSUMUTUKCMACPZQUKCMBVBQRSABTUAUKULUMUQCEZURCEUKUMVCULUKUPUBEUMVCIUCUDUP
    BCUEUFUGAUQCUHUIUJ $.
    $( [17-Apr-2008] $) $( [18-Oct-1999] $)

