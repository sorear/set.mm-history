$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y h $.
    $( Define the set of subspaces of a Hilbert space.  See
       ~ sh for its membership relation.  Basically, a subspace
       is a subset of a Hilbert space that acts like a vector space.
       From Definition of [Beran] p. 95. $)
    df-sh $a |- SH = { h | ( ( h (_ H~ /\ 0h e. h ) /\
       ( A. x e. h A. y e. h ( x +h y ) e. h /\
       A. x e. CC A. y e. h ( x .h y ) e. h ) ) } $.
  $}

  ${
    $d x y h H $.  $d x y A $.  $d x y B $.
    $( The set of subspaces of a Hilbert space exists (is a set). $)
    shex $p |- SH e. V $=
      ( vh vx vy csh cv chil wss c0v wcel wa cva co wral csm cc cab cvv df-sh
      cpw df-pw ax-hilex pwex eqeltrr simpll ss2abi ssexi eqeltr ) DAEZFGZHUHIZ
      JBEZCEZKLUHICUHMBUHMUKULNLUHICUHMBOMJZJZAPZQBCARUOUIAPZFSUPQAFTFUAUBUCUNU
      IAUIUJUMUDUEUFUG $.
      $( [23-Oct-1999] $)

    $( Subspace ` H ` of a Hilbert space.  A subspace is a subset of Hilbert
       space which contains the zero vector and is closed under vector
       addition and scalar multiplication.  Definition of [Beran] p. 95. $)
    sh $p |- ( H e. SH <-> ( ( H (_ H~ /\ 0h e. H ) /\
       ( A. x e. H A. y e. H ( x +h y ) e. H /\
       A. x e. CC A. y e. H ( x .h y ) e. H ) ) ) $=
      ( vh csh wcel cvv chil wss c0v wa cv cva co wral csm cc elisset ax-hilex
      ssex ad2antrr wceq sseq1 eleq2 anbi12d raleqd ralbidv df-sh elab2g
      pm5.21nii ) CEFCGFZCHIZJCFZKZALZBLZMNZCFZBCOZACOZUOUPPNZCFZBCOZAQOZKZKZCE
      RULUKUMVECHSTUADLZHIZJVGFZKZUQVGFZBVGOZAVGOZVAVGFZBVGOZAQOZKZKVFDCEGVGCUB
      ZVJUNVQVEVRVHULVIUMVGCHUCVGCJUDUEVRVMUTVPVDVLUSAVGCVKURBVGCVGCUQUDUFUFVRV
      OVCAQVNVBBVGCVGCVAUDUFUGUEUEABDUHUIUJ $.
      $( [16-Aug-1999] $)

    $( A subspace is a subset of Hilbert space. $)
    shss $p |- ( H e. SH -> H (_ H~ ) $=
      ( vx vy csh wcel chil wss c0v wa cv cva co wral csm cc sh pm3.26bd
      pm3.26d ) ADEZAFGZHAEZSTUAIBJZCJZKLAECAMBAMUBUCNLAECAMBOMIBCAPQR $.
      $( [9-Oct-1999] $)

    $( A member of a subspace of a Hilbert space is a vector. $)
    shelt $p |- ( ( H e. SH /\ A e. H ) -> A e. H~ ) $=
      ( csh wcel chil shss sseld imp ) BCDZABDAEDIBEABFGH $.
      $( [16-Dec-2004] $) $( [14-Dec-2004] $)

    ${
      shssi.1 $e |- H e. SH $.
      $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
      shssi $p |- H (_ H~ $=
        ( csh wcel chil wss shss ax-mp ) ACDAEFBAGH $.
        $( [6-Oct-1999] $)

      $( A member of a subspace of a Hilbert space is a vector. $)
      shel $p |- ( A e. H -> A e. H~ ) $=
        ( chil shssi sseli ) BDABCEF $.
        $( [6-Oct-1999] $)

      ${
        sheli.1 $e |- A e. H $.
        $( A member of a subspace of a Hilbert space is a vector. $)
        sheli $p |- A e. H~ $=
          ( chil shssi sselii ) BEABCFDG $.
          $( [6-Oct-1999] $)
      $}
    $}

    $( The zero vector belongs to any subspace of a Hilbert space. $)
    sh0 $p |- ( H e. SH -> 0h e. H ) $=
      ( vx vy csh wcel chil wss c0v wa cv cva co wral csm cc sh pm3.26bd
      pm3.27d ) ADEZAFGZHAEZSTUAIBJZCJZKLAECAMBAMUBUCNLAECAMBOMIBCAPQR $.
      $( [11-Oct-1999] $)

    $( Closure of vector addition in a subspace of a Hilbert space. $)
    shaddclt $p |- ( H e. SH ->
                   ( ( A e. H /\ B e. H ) -> ( A +h B ) e. H ) ) $=
      ( vx vy wcel wa cv cva co wral csh wceq opreq1 eleq1d opreq2 rcla42v csm
      cc chil wss c0v sh pm3.27bd pm3.26d syl5com ) ACFBCFGDHZEHZIJZCFZECKDCKZA
      BIJZCFZCLFZUJUMAUHIJZCFDEABCCUGAMUIUOCUGAUHINOUHBMUOULCUHBAIPOQUNUKUGUHRJ
      CFECKDSKZUNCTUAUBCFGUKUPGDECUCUDUEUF $.
      $( [13-Sep-1999] $)

    $( Closure of vector scalar multiplication in a subspace of a Hilbert
       space. $)
    shmulclt $p |- ( ( H e. SH /\ A e. CC /\ B e. H ) -> ( A .h B ) e. H ) $=
      ( vx vy csh wcel cc csm co wa cv wral wceq opreq1 eleq1d opreq2 rcla42v
      cva chil wss c0v sh pm3.27bd pm3.27d syl5com 3impib ) CFGZAHGZBCGZABIJZCG
      ZUIUJKDLZELZIJZCGZECMDHMZULUHUPULAUNIJZCGDEABHCUMANUOURCUMAUNIOPUNBNURUKC
      UNBAIQPRUHUMUNSJCGECMDCMZUQUHCTUAUBCGKUSUQKDECUCUDUEUFUG $.
      $( [21-Feb-2007] $) $( [13-Sep-1999] $)

    $( Closure of vector scalar multiplication in a subspace of a Hilbert
       space. $)
    shmulcltOLD $p |- ( H e. SH ->
                   ( ( A e. CC /\ B e. H ) -> ( A .h B ) e. H ) ) $=
      ( vx vy cc wcel wa cv csm co wral csh wceq opreq1 eleq1d opreq2 rcla42v
      cva chil wss c0v sh pm3.27bd pm3.27d syl5com ) AFGBCGHDIZEIZJKZCGZECLDFLZ
      ABJKZCGZCMGZUJUMAUHJKZCGDEABFCUGANUIUOCUGAUHJOPUHBNUOULCUHBAJQPRUNUGUHSKC
      GECLDCLZUKUNCTUAUBCGHUPUKHDECUCUDUEUF $.
      $( [13-Sep-1999] $)

    $( Closure of vector subtraction in a subspace of a Hilbert space. $)
    shsubclt $p |- ( H e. SH ->
                   ( ( A e. H /\ B e. H ) -> ( A -h B ) e. H ) ) $=
      ( csh wcel wa cmv co c1 cneg csm cva chil wceq shss sseld anim12d imp
      hvsubvalt syl shaddclt cc ax1cn negcl shmulcltOLD mpani sylan2d eqeltrd
      ex ) CDEZACEZBCEZFZABGHZCEUJUMFZUNAIJZBKHZLHZCUOAMEZBMEZFZUNURNUJUMVAUJUK
      USULUTUJCMACOZPUJCMBVBPQRABSTUJUMURCEZUJUKUQCEZVCULAUQCUAUJUPUBEULVDIUCUD
      UPBCUEUFUGRUHUI $.
      $( [18-Oct-1999] $)

    $(
    @( Reversal of subtraction remains a member of a subspace of a Hilbert
       space. @)
    shsubrclt @p  |- ( H e. SH -> ( ( A e. H~ /\ B e. H~ ) ->
                    ( ( A -h B ) e. H <-> ( B -h A ) e. H ) ) ) @=
?@.
    $)

    $( Subspace ` H ` of a Hilbert space. $)
    sh2 $p |- ( H (_ H~ -> ( H e. SH <-> ( 0h e. H /\
       ( A. x e. H A. y e. H ( x +h y ) e. H /\
       A. x e. CC A. y e. H ( x .h y ) e. H ) ) ) ) $=
      ( chil wss c0v wcel wa cv cva co wral csm cc csh anass baib sh syl5bb )
      CDEZTFCGZHAIZBIZJKCGBCLACLUBUCMKCGBCLANLHZHZUAUDHZCOGUETUFTUAUDPQABCRS $.
      $( [16-Aug-1999] $)
  $}

