$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Projectors
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d h f x y z w $.
    $( Define the projection function on a Hilbert space, as a mapping from the
       Hilbert lattice to a function on Hilbert space.  Every closed subspace
       is associated with a unique projection function.  Remark in [Kalmbach]
       p. 66, adopted as a definition. ` ( proj `` H ) `` A ` is the projection
       of vector ` A ` onto closed subspace ` H ` .  Note that the range of
       ` proj ` is the set of all projection operators, so ` T e. ran proj `
       means that ` T ` is a projection operator. $)
    df-pj $a |- proj = { <. h , f >. | ( h e. CH /\
           f = { <. x , y >. | ( x e. ~H /\
           y = U. { z e. h | E. w e. ( _|_ ` h ) x = ( z +h w ) } ) } ) } $.
  $}

  ${
    $d f h w x y z H $.  $d w x y z A $.  $d x y B $.
    $( The value of the projection map. $)
    pjmval $p |- ( H e. CH -> ( proj ` H ) = { <. x , y >. | ( x e. ~H /\
           y = U. { z e. H | E. w e. ( _|_ ` H ) x = ( z +h w ) } ) } ) $=
      ( vh vf cv chil wcel cva co wceq cort cfv wrex crab cuni wa copab cch
      cpj cab eleq2 fveq2 rexeqdv anbi12d abbidv df-rab 3eqtr4g unieqd eqeq2d
      anbi2d opabbidv df-pj ax-hilex opabex2 fvopab4 ) FGEAHZIJZBHZUSCHZDHKLMZD
      FHZNOZPZCVDQZRZMZSZABTUTVAVCDENOZPZCEQZRZMZSZABTUAUBVDEMZVJVPABVQVIVOUTVQ
      VHVNVAVQVGVMVQVBVDJZVFSZCUCVBEJZVLSZCUCVGVMVQVSWACVQVRVTVFVLVDEVBUDVQVCDV
      EVKVDENUEUFUGUHVFCVDUIVLCEUIUJUKULUMUNABCDGFUOABIVNUPUQUR $.
      $( [23-Oct-1999] $)

    $( Value of a projection. $)
    pjval $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) =
             U. { x e. H | E. y e. ( _|_ ` H ) A = ( x +h y ) } ) $=
      ( vz vw cch wcel chil wa cpj cfv cv cva co wceq cort wrex crab cuni
      copab pjmval fveq1d adantr cvv rabexg uniexg syl eqeq1 rexbidv rabbidv
      unieqd eqid fvopab4g sylan2 ancoms eqtrd ) DGHZCIHZJCDKLZLZCEMZIHFMVBAMBM
      NOZPZBDQLZRZADSZTZPJEFUAZLZCVCPZBVERZADSZTZURVAVJPUSURCUTVIEFABDUBUCUDUSU
      RVJVNPZURUSVNUEHZVOURVMUEHVPVLADGUFVMUEUGUHEFCVHVNIUEVIVBCPZVGVMVQVFVLADV
      QVDVKBVEVBCVCUIUJUKULVIUMUNUOUPUQ $.
      $( [23-Oct-1999] $)

    $( Closure of a projection in its subspace.  If we consider this together
       with ~ axpjpj to be axioms, the need for the ~ ax-hcompl can often be
       avoided for the kinds of theorems we are interested in here.  An
       interesting project is to see how far we can go by using them in place
       of it.  In particular, we can prove the orthomodular law ~ pjomli .) $)
    axpjcl $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) e. H ) $=
      ( vx vy cch wcel chil wa cpj cfv cv cva co wceq cort wrex crab cuni
      pjval wreu pjtheu reucl syl eqeltrd ) BEFAGFHZABIJJACKDKLMNDBOJPZCBQRZBCD
      ABSUEUFCBTUGBFCDABUAUFCBUBUCUD $.
      $( [23-Oct-1999] $)

$(
    @( Closure of projection, as an axiom.  Can be used to replace ~ axpjcl
       for studies of the Hilbert space completeness axiom. @)
    ax-pjcl @a |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) e. H ) @.
$)

    $( Equality with a projection. $)
    pjeq2 $p |- ( ( H e. CH /\ A e. ~H /\ B e. H ) ->
       ( ( ( proj ` H ) ` A ) = B <-> E. x e. ( _|_ ` H ) A = ( B +h x ) ) ) $=
      ( vy wcel cch chil cpj cfv wceq cv cva co cort wrex wb wa crab cuni
      pjval eqeq1d adantl wreu pjtheu opreq1 eqeq2d rexbidv reuuni2 sylan2
      bitr4d 3impb 3coml ) CDFZDGFZBHFZBDIJJZCKZBCALZMNZKZADOJZPZQZUNUOUPVDUNUO
      UPRZRURBELZUSMNZKZAVBPZEDSTZCKZVCVEURVKQUNVEUQVJCEABDUAUBUCVEUNVIEDUDVCVK
      QEABDUEVIVCEDCVFCKZVHVAAVBVLVGUTBVFCUSMUFUGUHUIUJUKULUM $.
      $( [20-Jan-2007] $)

    $( Equality with a projection. $)
    pjeq $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( ( proj ` H ) ` A ) = B <->
                ( B e. H /\ E. x e. ( _|_ ` H ) A = ( B +h x ) ) ) ) $=
      ( cch wcel chil wa cpj cfv wceq cv cva co cort wrex eleq1 axpjcl syl5cbi
      pm4.71rd wb pjeq2 3expa pm5.32da bitrd ) DEFZBGFZHZBDIJJZCKZCDFZUJHUKBCAL
      MNKADOJPZHUHUJUKUJUIDFUKUHUICDQBDRSTUHUKUJULUFUGUKUJULUAABCDUBUCUDUE $.
      $( [20-Jan-2007] $)
  $}

  $( Closure of a projection in Hilbert space. $)
  pjhcl $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) e. ~H ) $=
    ( cch wcel chil wa cpj cfv wss chss adantr axpjcl sseldd ) BCDZAEDZFBEABGHH
    NBEIOBJKABLM $.
    $( [30-Oct-1999] $)

