$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Projectors
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d h x y z $.
    $( Define the projection function on a Hilbert space, as a mapping from the
       Hilbert lattice to a function on Hilbert space.  Every closed subspace
       is associated with a unique projection function.  Remark in [Kalmbach]
       p. 66, adopted as a definition. ` ( proj `` H ) `` A ` is the projection
       of vector ` A ` onto closed subspace ` H ` .  Note that the range of
       ` proj ` is the set of all projection operators, so ` T e. ran proj `
       means that ` T ` is a projection operator. $)
    df-pj $a |- proj = ( h e. CH |-> ( x e. ~H |->
           ( iota_ z e. h E. y e. ( _|_ ` h ) x = ( z +h y ) ) ) ) $.
  $}

  ${
    $d h x y z H $.  $d x y z A $.  $d x y z B $.
    $( The value of the projection map.  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    pjfval $p |- ( H e. CH -> ( proj ` H ) = ( x e. ~H |->
           ( iota_ z e. H E. y e. ( _|_ ` H ) x = ( z +h y ) ) ) ) $=
      ( vh chil cv cva co wceq cort cfv wrex crio cmpt cch cpj wcel id cvv
      fveq2 rexeqdv riotaeqbidv adantr mpteq2dva df-pj ax-hilex mptexg ax-mp
      fvmpt ) EDAFAGZCGBGHIJZBEGZKLZMZCUMNZOAFULBDKLZMZCDNZOZPQUMDJZAFUPUSVAUPU
      SJUKFRVAUOURCUMDVASVAULBUNUQUMDKUAUBUCUDUEABCEUFFTRUTTRUGAFUSTUHUIUJ $.
      $( [15-Dec-2013] $) $( [23-Oct-1999] $)

    $( Value of a projection.  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    pjval $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) =
             ( iota_ x e. H E. y e. ( _|_ ` H ) A = ( x +h y ) ) ) $=
      ( vz cch wcel chil cpj cfv cv cva wceq cort wrex crio cmpt pjfval fveq1d
      co eqeq1 rexbidv riotabidv eqid riotaex fvmpt sylan9eq ) DFGZCHGCDIJZJCEH
      EKZAKBKLTZMZBDNJZOZADPZQZJCUKMZBUMOZADPZUHCUIUPEBADRSECUOUSHUPUJCMZUNURAD
      UTULUQBUMUJCUKUAUBUCUPUDURADUEUFUG $.
      $( [15-Dec-2013] $) $( [23-Oct-1999] $)

    $( Closure of a projection in its subspace.  If we consider this together
       with ~ axpjpj to be axioms, the need for the ~ ax-hcompl can often be
       avoided for the kinds of theorems we are interested in here.  An
       interesting project is to see how far we can go by using them in place
       of it.  In particular, we can prove the orthomodular law ~ pjomli .) $)
    axpjcl $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) e. H ) $=
      ( vx vy cch wcel chil wa cpj cfv cv cva co wceq cort wrex crio pjval wreu
      pjtheu riotacl syl eqeltrd ) BEFAGFHZABIJJACKDKLMNDBOJPZCBQZBCDABRUDUECBS
      UFBFCDABTUECBUAUBUC $.
      $( [15-Dec-2013] $) $( [23-Oct-1999] $)

$(
    @( Closure of projection, as an axiom.  Can be used to replace ~ axpjcl
       for studies of the Hilbert space completeness axiom. @)
    ax-pjcl @a |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) e. H ) @.
$)

    $( Equality with a projection. $)
    pjeq2 $p |- ( ( H e. CH /\ A e. ~H /\ B e. H ) ->
       ( ( ( proj ` H ) ` A ) = B <-> E. x e. ( _|_ ` H ) A = ( B +h x ) ) ) $=
      ( vy vz cch wcel chil w3a cpj cfv wceq cv cva co cort wrex wb 3adant3 wa
      crio pjval eqeq1d wreu simp3 pjtheu opreq1 eqeq2d rexbidv riota2f syl2anc
      ax-17 a17d bitr4d ) DGHZBIHZCDHZJZBDKLLZCMZBENZANZOPZMZADQLZRZEDUBZCMZBCV
      COPZMZAVFRZUPUQVAVISURUPUQUAUTVHCEABDUCUDTUSURVGEDUEZVLVISUPUQURUFUPUQVMU
      REABDUGTVGVLEFDCFNCHEUMURVLEUNVBCMZVEVKAVFVNVDVJBVBCVCOUHUIUJUKULUO $.
      $( [15-Dec-2013] $) $( [20-Jan-2007] $)

    $( Equality with a projection. $)
    pjeq $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( ( proj ` H ) ` A ) = B <->
                ( B e. H /\ E. x e. ( _|_ ` H ) A = ( B +h x ) ) ) ) $=
      ( cch wcel chil wa cpj cfv wceq cv cva co cort wrex axpjcl eleq1 pm4.71rd
      syl5ibcom wb pjeq2 3expa pm5.32da bitrd ) DEFZBGFZHZBDIJJZCKZCDFZUJHUKBCA
      LMNKADOJPZHUHUJUKUHUIDFUJUKBDQUICDRTSUHUKUJULUFUGUKUJULUAABCDUBUCUDUE $.
      $( [20-Jan-2007] $)
  $}

  $( Closure of a projection in Hilbert space. $)
  pjhcl $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) e. ~H ) $=
    ( cch wcel chil wa cpj cfv wss chss adantr axpjcl sseldd ) BCDZAEDZFBEABGHH
    NBEIOBJKABLM $.
    $( [30-Oct-1999] $)

