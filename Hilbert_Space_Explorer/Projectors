$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Projectors
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d h x y z $.
    $( Define the projection function on a Hilbert space, as a mapping from the
       Hilbert lattice to a function on Hilbert space.  Every closed subspace
       is associated with a unique projection function.  Remark in [Kalmbach]
       p. 66, adopted as a definition. ` ( proj `` H ) `` A ` is the projection
       of vector ` A ` onto closed subspace ` H ` .  Note that the range of
       ` proj ` is the set of all projection operators, so ` T e. ran proj `
       means that ` T ` is a projection operator. $)
    df-pj $a |- proj = ( h e. CH |-> ( x e. ~H |->
           ( iota_ z e. h E. y e. ( _|_ ` h ) x = ( z +h y ) ) ) ) $.
  $}

  ${
    $d h x y z H $.  $d x y z A $.  $d x y z B $.
    $( The value of the projection map.  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    pjfval $p |- ( H e. CH -> ( proj ` H ) = ( x e. ~H |->
           ( iota_ z e. H E. y e. ( _|_ ` H ) x = ( z +h y ) ) ) ) $=
      ( vh chil cv cva co wceq cort cfv wrex crio cmpt cch cpj wcel id fveq2
      rexeqdv riotaeqbidv adantr mpteq2dva df-pj ax-hilex mptex fvmpt ) EDAFAGZ
      CGBGHIJZBEGZKLZMZCUKNZOAFUJBDKLZMZCDNZOPQUKDJZAFUNUQURUNUQJUIFRURUMUPCUKD
      URSURUJBULUOUKDKTUAUBUCUDABCEUEAFUQUFUGUH $.
      $( [15-Dec-2013] $) $( [23-Oct-1999] $)

    $( Value of a projection.  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    pjval $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) =
             ( iota_ x e. H E. y e. ( _|_ ` H ) A = ( x +h y ) ) ) $=
      ( vz cch wcel chil cpj cfv cv cva wceq cort wrex crio cmpt pjfval fveq1d
      co eqeq1 rexbidv riotabidv eqid riotaex fvmpt sylan9eq ) DFGZCHGCDIJZJCEH
      EKZAKBKLTZMZBDNJZOZADPZQZJCUKMZBUMOZADPZUHCUIUPEBADRSECUOUSHUPUJCMZUNURAD
      UTULUQBUMUJCUKUAUBUCUPUDURADUEUFUG $.
      $( [15-Dec-2013] $) $( [23-Oct-1999] $)

    $( Equality with a projection.  This version of ~ pjeq does not assume the
       Axiom of Choice via ~ pjth .  (Contributed by Mario Carneiro,
       15-May-2014.) $)
    pjpreeq $p |- ( ( H e. CH /\ A e. ( H +H ( _|_ ` H ) ) ) ->
                    ( ( ( proj ` H ) ` A ) = B <->
                      ( B e. H /\ E. x e. ( _|_ ` H ) A = ( B +h x ) ) ) ) $=
      ( vy vz cch wcel cfv co wa cv cva wceq wrex csh wb syl syl2anc chil shsel
      cort cph crio cpj wreu wmo chsh shocsh biimpa cin c0h ocin pjthmo syl3anc
      adantr reu5 sylanbrc eleq1 syl5ibcom pm4.71rd wss shsss ssel2 sylan pjval
      riotacl syldan eqeq1d id ax-17 a17d oveq1 eqeq2d rexbidv riota2f pm5.32da
      syl2anr 3bitr4d ) DGHZBDDUBIZUCJZHZKZBELZALZMJZNZAWAOZEDUDZCNZCDHZWKKBDUE
      IIZCNWLBCWFMJZNZAWAOZKWDWKWLWDWJDHZWKWLWDWIEDUFZWQWDWIEDOZWEDHWIKEUGZWRVT
      WCWSVTDPHZWAPHZWCWSQDUHZVTXAXBXCDUIRZEADWABUASUJVTWTWCVTXAXBDWAUKULNZWTXC
      XDVTXAXEXCDUMREADWABUNUOUPWIEDUQURZWIEDVGRWJCDUSUTVAWDWMWJCVTWCBTHZWMWJNV
      TWBTVBZWCXGVTXAXBXHXCXDDWAVCSWBTBVDVEEABDVFVHVIWDWLWPWKWLWLWRWPWKQWDWLVJX
      FWIWPEFDCFLCHEVKWLWPEVLWECNZWHWOAWAXIWGWNBWECWFMVMVNVOVPVRVQVS $.
      $( [15-Dec-2013] $)

    $( Equality with a projection. $)
    pjeq $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( ( proj ` H ) ` A ) = B <->
                ( B e. H /\ E. x e. ( _|_ ` H ) A = ( B +h x ) ) ) ) $=
      ( cch wcel chil cort cfv cph co cpj wceq cv cva wrex wa wb pjth eleq2d
      biimpar pjpreeq syldan ) DEFZBGFZBDDHIZJKZFZBDLIICMCDFBCANOKMAUFPQRUDUHUE
      UDUGGBDSTUAABCDUBUC $.
      $( [15-May-2014] $) $( [20-Jan-2007] $)

    $( Closure of a projection in its subspace.  If we consider this together
       with ~ axpjpj to be axioms, the need for the ~ ax-hcompl can often be
       avoided for the kinds of theorems we are interested in here.  An
       interesting project is to see how far we can go by using them in place
       of it.  In particular, we can prove the orthomodular law ~ pjomli .) $)
    axpjcl $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) e. H ) $=
      ( vx cch wcel chil wa cpj cfv cv co wceq cort wrex eqid pjeq mpbii simpld
      cva ) BDEAFEGZABHIIZBEZAUACJSKLCBMINZTUAUALUBUCGUAOCAUABPQR $.
      $( [15-May-2014] $) $( [23-Oct-1999] $)

$(
    @( Closure of projection, as an axiom.  Can be used to replace ~ axpjcl
       for studies of the Hilbert space completeness axiom. @)
    ax-pjcl @a |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) e. H ) @.
$)
  $}

  $( Closure of a projection in Hilbert space. $)
  pjhcl $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` H ) ` A ) e. ~H ) $=
    ( cch wcel chil wa cpj cfv wss chss adantr axpjcl sseldd ) BCDZAEDZFBEABGHH
    NBEIOBJKABLM $.
    $( [30-Oct-1999] $)

