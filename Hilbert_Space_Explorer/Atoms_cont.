$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Atoms (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    atcvat3.1 $e |- A e. CH $.
    $( A condition implying that a certain lattice element is an atom.  Part of
       Lemma 3.2.20 of [PtakPulmannova] p. 68. $)
    atcvat3i $p |- ( ( B e. Atoms /\ C e. Atoms ) ->
          ( ( ( -. B = C /\ -. C C_ A ) /\ B C_ ( A vH C ) ) ->
          ( A i^i ( B vH C ) ) e. Atoms ) ) $=
      ( cat wcel wa wceq wn wss chj co cin ccv wbr wi cch wb chcv1 mpan biimpa
      ad2ant2lr chjcom opreq2d chjass mp3an1 ancoms eqtr4d adantr w3a chlej2
      ex pm3.26 chjcl adantl syl3anc imp eqsstrd chjidm syl ad2antlr sseqtrd
      mp3anl3 pm3.27 jca chub2 sylanc eqssd atelch anim12i sylan breq2d
      adantrl mpbird jctil syl2an cvexch sylibrd atcvat2 chincl expdimp syld
      exp4b imp4c ) BEFZCEFZGZBCHIZCAJIZBACKLZJZABCKLZMZEFZWGWHWIWKWNWGWHGWIWKG
      ZWMWLNOZWNWGWOWPPWHWGWOAAWLKLZNOZWPWGWOWRWGWOGWRAWJNOZWFWIWSWEWKWFWIWSAQF
      ZWFWIWSRDACSTUAUBWGWKWRWSRWIWGWKGWQWJANBQFZCQFZGZWKWQWJHWGXCWKGZWQWJXDWQW
      JWJKLZWJXDWQWJBKLZXEXCWQXFHWKXCWQACBKLZKLZXFXCWLXGAKBCUCUDXBXAXFXHHZWTXBX
      AXIDACBUEUFUGUHUIXCWKXFXEJZXAWJQFZXKWKXJPXCXAXKXKUJWKXJBWJWJUKULXAXBUMXBX
      KXAWTXBXKDACUNTZUOZXMUPUQURXBXEWJHZXAWKXBXKXNXLWJUSUTVAVBXCWJWQJZWKXBWLQF
      ZGCWLJZXOXCXBXPWTXQXODCWLAUKVCXCXBXPXAXBVDBCUNZVEXBXAXQCBVFUGVGUIVHWEXAWF
      XBBVIZCVIZVJVKVLVMVNULWGWTXPGZWPWRRXAXBYAWEWFXCXPWTXRDVOZXSXTVPAWLVQUTVRU
      IWGWHWPWNWMQFZWEWFWHWPGWNPWGWMBCVSXAXBYCWEWFXCYAYCYBAWLVTUTXSXTVPWEWFUMWE
      WFVDUPWAWBWCWD $.
      $( [2-Jul-2004] $)

    $( A condition implying existence of an atom with the properties shown.
       Lemma 3.2.20 of [PtakPulmannova] p. 68. $)
    atcvat4i $p |- ( ( B e. Atoms /\ C e. Atoms ) ->
          ( ( A =/= 0H /\ B C_ ( A vH C ) ) ->
          E. x e. Atoms ( x C_ A /\ B C_ ( C vH x ) ) ) ) $=
      ( cat wcel wa wceq wss c0h wne chj co cv wrex wi sseq1 cch chub1 atelch
      syl2an syl5bir exp3a impcom anim2d com23 reximdvai hatomici syl5 ex a1i
      com4l imp4a adantl wb chlejb2 mpan2 biimpa sseq2d expl chub2 jctird
      pm3.26 jctild imp anassrs opreq2 anbi12d rcla4ev syl adantrl exp31 wo wn
      cin atcvat3i w3a atexch ad2antlr simpll 3jca inss2 chjcom sseqtrd adantr
      atnssm0 mpan chincl pm3.27 chjcl sylanc chle0 inss1 sslin ax-mp incom
      sseqtri sseq2 mpbii syl5bi sylbid adantrr jca sylc jctil jcad syl6 ioran
      syl5ib syl7 ecase3d ) CFGZDFGZHZCDIZDBJZBKLZCBDMNZJZHZAOZBJZCDYBMNZJZHZAF
      PZQZXNXPYHQXMXNXPXRXTYGXTXNXPXRYGXNXPXRYGQZQQXTXNXPYIXNXPHZYCAFPYGXRYJYCY
      FAFYJYCYBFGZYFYJYCYKYFYJYKYEYCXPXNYKYEQXPXNYKYEXPYEDYDJZXNYKHCDYDRDSGZYBS
      GYLXNYKDYBTDUAZYBUAUBUCUDUEUFUDUGUHABEUIUJUKULUMUNUOXOXQYAYGXOXQHZXTYGXRY
      OXTHXMCBJZCDCMNZJZHZHZYGXOXQXTYTXOXQXTHZYTXOUUAYSXMCSGZYMUUAYSQXMXNUUBYMH
      ZUUAYPYRYMUUAYPQUUBYMXQXTYPYMXQHZXTYPUUDXSBCYMXQXSBIZYMBSGZXQUUEUPEDBUQUR
      USUTUSVAUOCDVBVCCUAZYNUBXMXNVDVEVFVGYFYSACFYBCIZYCYPYEYRYBCBRUUHYDYQCYBCD
      MVHUTVIVJVKVLVMXOXPXQVNVOZXTYGYAXOXPVOZXQVOZHZXTYGQUUIXOUULXTYGXOUULXTHZB
      CDMNZVPZFGZUUOBJZCDUUOMNZJZHZHYGXOUUMUUPUUTBCDEVQZXOUUMUUTXOUUMHZUUSUUQYM
      UUPXMVRUUOYQJZDUUOVPZKIZHUUSUVBDUUOCVSUVBYMUUPXMXNYMXMUUMYNVTXOUUMUUPUVAV
      FXMXNUUMWAWBUVBUVCUVEXOUVCUUMXOUUOUUNYQUUOUUNJXOBUUNWCULUUBYMUUNYQIXMXNCD
      WDUUGYNUBWEWFXOUULUVEXTXOUUKUVEUUJXOUUKUVEXOUUKBDVPZKIZUVEXNUUKUVGUPZXMUU
      FXNUVHEBDWGWHUOXOUVDKJZUVEUVGXOUVDSGZUVIUVEUPUUBYMUVJXMXNYMUUOSGZUVJUUCDU
      UOWIUUBYMWJUUCUUNSGZUVKCDWKUUFUVLUVKEBUUNWIWHVKWLUUGYNUBUVDWMVKUVGUVDUVFJ
      UVIUVDDBVPZUVFUUQUVDUVMJBUUNWNZUUOBDWOWPDBWQWRUVFKUVDWSWTXAXBVFVLXCXDXEUV
      NXFUKXGYFUUTAUUOFYBUUOIZYCUUQYEUUSYBUUOBRUVOYDUURCYBUUODMVHUTVIVJXHUDXPXQ
      XIXJXRXTWJXKXL $.
      $( [22-Apr-2007] $) $( [2-Jul-2004] $)
  $}

  $( Two Hilbert lattice elements have the dual modular pair property if the
     first is an atom.  Theorem 7.6(c) of [MaedaMaeda] p. 31. $)
  atdmd $p |- ( ( A e. Atoms /\ B e. CH ) -> A MH* B ) $=
    ( cat wcel cch wa cin c0h wceq cdmd wbr chj co ccv wb cvp chjcom atelch
    sylan2 breq2d bitrd ancoms wi cvdmd 3expia sylan sylbid wn wss atnssm0
    con1bid ssdmd1 pm2.61d ) ACDZBEDZFZBAGHIZABJKZUPUQBABLMZNKZURUOUNUQUTOUOUNF
    ZUQBBALMZNKUTBAPVAVBUSBNUOAEDZVBUSIUNBAQARZSTUAUBVCUOUTURUCUNVCUOUTURABUDUE
    VDUFUGUPUQUHABUIZURUPVEUQUOUNVEUHUQOBAUJUBUKVCUOVEURUCUNVCUOVEURABULUEVDUFU
    GUM $.
    $( [27-Jul-2007] $) $( [22-Jun-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Two Hilbert lattice elements have the modular pair property if the first
       is an atom.  Theorem 7.6(b) of [MaedaMaeda] p. 31. $)
    atmd $p |- ( ( A e. Atoms /\ B e. CH ) -> A MH B ) $=
      ( vx cat wcel cch wa cv cmd wbr wral cdmd atdmd r19.21aiva wb atelch
      mddmd2 syl mpbird adantr wi breq2 rcla4v adantl mpd ) ADEZBFEZGACHZIJZCFK
      ZABIJZUFUJUGUFUJAUHLJZCFKZUFULCFAUHMNUFAFEUJUMOAPCAQRSTUGUJUKUAUFUIUKCBFU
      HBAIUBUCUDUE $.
      $( [12-Aug-2005] $) $( [22-Jun-2004] $)
  $}

  $( Two Hilbert lattice elements have the dual modular pair property if the
     second is an atom.  Part of Exercise 6 of [Kalmbach] p. 103. $)
  atmd2 $p |- ( ( A e. CH /\ B e. Atoms ) -> A MH B ) $=
    ( cch wcel cat wa cin c0h wceq cmd wbr chj co ccv cvp wi cvexch cvmd
    3expia sylbird atelch sylan2 sylbid wn wss atnssm0 con1bid ssmd2 3com12
    syl3an2 pm2.61d ) ACDZBEDZFZABGZHIZABJKZUNUPAABLMNKZUQABOULBCDZURUQPUMULUSF
    URUOBNKZUQABQULUSUTUQABRSTBUAZUBUCUNUPUDBAUEZUQUNVBUPABUFUGULUMVBUQULUSVBUQ
    UMUSULVBUQBAUHUIVAUJSUCUK $.
    $( [23-Sep-2005] $) $( [22-Jun-2004] $)

  ${
    atabs.1 $e |- A e. CH $.
    atabs.2 $e |- B e. CH $.
    $( Absorption of an incomparable atom.  Similar to Exercise 7.1 of
       [MaedaMaeda] p. 34. $)
    atabsi $p |- ( C e. Atoms -> ( -. C C_ ( A vH B ) ->
                ( ( A vH C ) i^i B ) = ( A i^i B ) ) ) $=
      ( cat wcel chj co wss wn cin wceq wa chub1i cch cmd wbr wi chjcli w3a
      mdi exp32 mp3an23 atelch atmd mpan2 sylc mpi adantr c0h wb atnssm0 mpan
      biimpa incom syl5eq opreq2d chj0i syl6eq eqtrd ineq1d inass chjcomi
      ineq1i chabs2i 3eqtri ineq2i eqtr2i ex ) CFGZCABHIZJKZACHIZBLZABLZMVKVMNZ
      VNVLLZBLZVPVOVQVRABVQVRACVLLZHIZAVKVRWAMZVMVKAVLJZWBABDEOCPGZCVLQRZWCWBSZ
      VKWDVLPGZAPGZWEWFSABDETZDWDWGWHUAWEWCWBCVLAUBUCUDCUEVKWGWEWICVLUFUGUHUIUJ
      VQWAAUKHIAVQVTUKAHVQVLCLZUKVTVKVMWJUKMZWGVKVMWKULWIVLCUMUNUOCVLUPUQURADUS
      UTVAVBVSVNVLBLZLVOVNVLBVCWLBVNWLBAHIZBLBWMLBVLWMBABDEVDVEWMBUPBAEDVFVGVHV
      IUQVJ $.
      $( [29-Dec-2005] $) $( [15-Jul-2004] $)

    $( Absorption of an incomparable atom. $)
    atabs2i $p |- ( C e. Atoms -> ( -. C C_ ( A vH B ) ->
                ( ( A vH C ) i^i ( A vH B ) ) = A ) ) $=
      ( cat wcel chj co wss wn cin wceq chjcli atabsi chjassi chjidmi opreq1i
      eqtr3i sseq2i notbii chabs2i eqeq2i 3imtr3g ) CFGCAABHIZHIZJZKACHIUELZAUE
      LZMCUEJZKUHAMAUECDABDENOUGUJUFUECAAHIZBHIUFUEAABDDEPUKABHADQRSTUAUIAUHABD
      EUBUCUD $.
      $( [19-Feb-2006] $) $( [18-Jul-2004] $)
  $}

