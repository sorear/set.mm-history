$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Atoms (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    atcvat3.1 $e |- A e. CH $.
    $( A condition implying that a certain lattice element is an atom.  Part of
       Lemma 3.2.20 of [PtakPulmannova] p. 68. $)
    atcvat3i $p |- ( ( B e. HAtoms /\ C e. HAtoms ) ->
          ( ( ( -. B = C /\ -. C C_ A ) /\ B C_ ( A vH C ) ) ->
          ( A i^i ( B vH C ) ) e. HAtoms ) ) $=
      ( cat wcel wa wceq wn wss chj co ccv wbr wi cch wb mpan atelch adantr cin
      chcv1 biimpa ad2ant2lr anim12i chjcom opreq2d chjass mp3an1 ancoms eqtr4d
      simpl chjcl adantl w3a chlej2 ex syl111anc imp eqsstrd chjidm syl sseqtrd
      ad2antlr simpr chub2 mp3anl3 syl21anc breq2d adantrl mpbird syl2an cvexch
      eqssd sylan jctil sylibrd chincl sylancr atcvat2 expdimp syld exp4b imp4c
      ) BEFZCEFZGZBCHIZCAJIZBACKLZJZABCKLZUAZEFZWGWHWIWKWNWGWHGWIWKGZWMWLMNZWNW
      GWOWPOWHWGWOAAWLKLZMNZWPWGWOWRWGWOGWRAWJMNZWFWIWSWEWKWFWIWSAPFZWFWIWSQDAC
      UBRUCUDWGWKWRWSQWIWGWKGWQWJAMWGBPFZCPFZGZWKWQWJHWEXAWFXBBSZCSZUEXCWKGZWQW
      JXFWQWJWJKLZWJXFWQWJBKLZXGXCWQXHHWKXCWQACBKLZKLZXHXCWLXIAKBCUFUGXBXAXHXJH
      ZWTXBXAXKDACBUHUIUJUKTXCWKXHXGJZXCXAWJPFZXMWKXLOXAXBULXBXMXAWTXBXMDACUMRZ
      UNZXOXAXMXMUOWKXLBWJWJUPUQURUSUTXBXGWJHZXAWKXBXMXPXNWJVAVBVDVCXCWJWQJZWKX
      CXBWLPFZCWLJZXQXAXBVEBCUMZXBXAXSCBVFUJXBXRWTXSXQDCWLAUPVGVHTVNVOVIVJVKUQW
      GWTXRGZWPWRQWEXAXBYAWFXDXEXCXRWTXTDVPVLAWLVMVBVQTWGWHWPWNWGWMPFZWEWFWHWPG
      WNOWEXAXBYBWFXDXEXCWTXRYBDXTAWLVRVSVLWEWFULWEWFVEWMBCVTURWAWBWCWD $.
      $( [2-Jul-2004] $)

    $( A condition implying existence of an atom with the properties shown.
       Lemma 3.2.20 of [PtakPulmannova] p. 68. $)
    atcvat4i $p |- ( ( B e. HAtoms /\ C e. HAtoms ) ->
          ( ( A =/= 0H /\ B C_ ( A vH C ) ) ->
          E. x e. HAtoms ( x C_ A /\ B C_ ( C vH x ) ) ) ) $=
      ( cat wcel wa wceq wss c0h chj co wi cch atelch syl2an sseq1 exp3a cin cv
      wne wrex hatomici chub1 syl5ibr impcom anim2d com23 reximdvai syl5 ex a1i
      com4l imp4a adantl wb chlejb2 mpan2 biimpa sseq2d expl chub2 jctird simpl
      jctild imp anassrs opreq2 anbi12d rcla4ev adantrl exp31 wo wn simpr ioran
      syl atcvat3i w3a ad2antlr simpll 3jca chjcom syl5sseq adantr atnssm0 mpan
      inss2 inss1 sslin ax-mp incom sseqtri sseq2 mpbii chincl sylancr syl11anc
      chjcl chle0 syl5ib sylbid adantrr atexch sylc jctil jcad syl6 syl5bi syl7
      jca ecase3d ) CFGZDFGZHZCDIZDBJZBKUBZCBDLMZJZHZAUAZBJZCDYCLMZJZHZAFUCZNZX
      OXQYINXNXOXQXSYAYHYAXOXQXSYHXOXQXSYHNZNNYAXOXQYJXSYDAFUCXOXQHZYHABEUDYKYD
      YGAFYKYDYCFGZYGYKYDYLYGYKYLYFYDXQXOYLYFNXQXOYLYFXOYLHYFXQDYEJZXODOGZYCOGY
      MYLDPZYCPDYCUEQCDYERUFSUGUHSUIUJUKULUMUNUOUPXPXRYBYHXPXRHZYAYHXSYPYAHXNCB
      JZCDCLMZJZHZHZYHXPXRYAUUAXPXRYAHZUUAXPUUBYTXNXNCOGZYNUUBYTNXOCPZYOUUCYNHZ
      UUBYQYSYNUUBYQNUUCYNXRYAYQYNXRHZYAYQUUFXTBCYNXRXTBIZYNBOGZXRUUGUQEDBURUSU
      TVAUTVBUPCDVCVDQXNXOVEVFVGVHYGYTACFYCCIZYDYQYFYSYCCBRUUIYEYRCYCCDLVIVAVJV
      KVRVLVMYBYAXPXQXRVNVOZYHXSYAVPUUJXQVOZXRVOZHZXPYAYHNXQXRVQXPUUMYAYHXPUUMY
      AHZBCDLMZTZFGZUUPBJZCDUUPLMZJZHZHYHXPUUNUUQUVABCDEVSZXPUUNUVAXPUUNHZUUTUU
      RUVCYNUUQXNVTUUPYRJZDUUPTZKIZHUUTUVCYNUUQXNXOYNXNUUNYOWAXPUUNUUQUVBVGXNXO
      UUNWBWCUVCUVDUVFXPUVDUUNXPUUOUUPYRBUUOWIXNUUCYNUUOYRIXOUUDYOCDWDQWEWFXPUU
      MUVFYAXPUULUVFUUKXPUULUVFXPUULBDTZKIZUVFXOUULUVHUQZXNUUHXOUVIEBDWGWHUPUVH
      UVEKJZXPUVFUVHUVEUVGJUVJUVEDBTZUVGUURUVEUVKJBUUOWJZUUPBDWKWLDBWMWNUVGKUVE
      WOWPXPUVEOGZUVJUVFUQXNUUCYNUVMXOUUDYOUUEYNUUPOGZUVMUUCYNVPUUEUUHUUOOGUVNE
      CDWTBUUOWQWRDUUPWQWSQUVEXAVRXBXCVGVLXDXLDUUPCXEXFUVLXGULXHYGUVAAUUPFYCUUP
      IZYDUURYFUUTYCUUPBRUVOYEUUSCYCUUPDLVIVAVJVKXISXJXKXM $.
      $( [22-Apr-2007] $) $( [2-Jul-2004] $)
  $}

  $( Two Hilbert lattice elements have the dual modular pair property if the
     first is an atom.  Theorem 7.6(c) of [MaedaMaeda] p. 31. $)
  atdmd $p |- ( ( A e. HAtoms /\ B e. CH ) -> A MH* B ) $=
    ( cat wcel cch wa cin c0h wceq wbr chj co ccv wb ancoms 3expia sylan sylbid
    wi wn cdmd cvp atelch chjcom sylan2 breq2d bitrd wss atnssm0 con1bid ssdmd1
    cvdmd pm2.61d ) ACDZBEDZFZBAGHIZABUAJZUPUQBABKLZMJZURUOUNUQUTNUOUNFZUQBBAKL
    ZMJUTBAUBVAVBUSBMUNUOAEDZVBUSIAUCZBAUDUEUFUGOUNVCUOUTURSVDVCUOUTURABULPQRUP
    UQTABUHZURUPVEUQUOUNVETUQNBAUIOUJUNVCUOVEURSVDVCUOVEURABUKPQRUM $.
    $( [27-Jul-2007] $) $( [22-Jun-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Two Hilbert lattice elements have the modular pair property if the first
       is an atom.  Theorem 7.6(b) of [MaedaMaeda] p. 31. $)
    atmd $p |- ( ( A e. HAtoms /\ B e. CH ) -> A MH B ) $=
      ( vx cat wcel cch wa cmd wbr wral cdmd atdmd r19.21aiva atelch mddmd2 syl
      cv wb mpbird adantr wi breq2 rcla4v adantl mpd ) ADEZBFEZGACQZHIZCFJZABHI
      ZUFUJUGUFUJAUHKIZCFJZUFULCFAUHLMUFAFEUJUMRANCAOPSTUGUJUKUAUFUIUKCBFUHBAHU
      BUCUDUE $.
      $( [12-Aug-2005] $) $( [22-Jun-2004] $)
  $}

  $( Two Hilbert lattice elements have the dual modular pair property if the
     second is an atom.  Part of Exercise 6 of [Kalmbach] p. 103. $)
  atmd2 $p |- ( ( A e. CH /\ B e. HAtoms ) -> A MH B ) $=
    ( cch wcel cat cin c0h wceq cmd wbr chj ccv cvp atelch cvexch 3expia sylbid
    wa co wi sylbird sylan2 wn wss atnssm0 con1bid ssmd2 3com12 syl3an2 pm2.61d
    cvmd ) ACDZBEDZRZABFZGHZABIJZUNUPAABKSLJZUQABMUMULBCDZURUQTBNZULUSRURUOBLJZ
    UQABOULUSVAUQABUKPUAUBQUNUPUCBAUDZUQUNVBUPABUEUFULUMVBUQUMULUSVBUQUTUSULVBU
    QBAUGUHUIPQUJ $.
    $( [23-Sep-2005] $) $( [22-Jun-2004] $)

  ${
    atabs.1 $e |- A e. CH $.
    atabs.2 $e |- B e. CH $.
    $( Absorption of an incomparable atom.  Similar to Exercise 7.1 of
       [MaedaMaeda] p. 34. $)
    atabsi $p |- ( C e. HAtoms -> ( -. C C_ ( A vH B ) ->
                ( ( A vH C ) i^i B ) = ( A i^i B ) ) ) $=
      ( cat wcel chj co wss wn cin wceq wa inass incom cch wi c0h syl5eq ineq1i
      chjcomi chabs2i 3eqtri ineq2i eqtr2i chub1i cmd wbr atelch atmd mpan2 w3a
      chjcli mdi exp32 mp3an23 sylc adantr wb atnssm0 mpan biimpa opreq2d chj0i
      mpi syl6eq eqtrd ineq1d ex ) CFGZCABHIZJKZACHIZBLZABLZMVKVMNZVOVNVLLZBLZV
      PVSVNVLBLZLVOVNVLBOVTBVNVTBAHIZBLBWALBVLWABABDEUBUAWABPBAEDUCUDUEUFVQVRAB
      VQVRACVLLZHIZAVKVRWCMZVMVKAVLJZWDABDEUGVKCQGZCVLUHUIZWEWDRZCUJVKVLQGZWGAB
      DEUNZCVLUKULWFWIAQGZWGWHRWJDWFWIWKUMWGWEWDCVLAUOUPUQURVFUSVQWCASHIAVQWBSA
      HVQWBVLCLZSCVLPVKVMWLSMZWIVKVMWMUTWJVLCVAVBVCTVDADVEVGVHVITVJ $.
      $( [29-Dec-2005] $) $( [15-Jul-2004] $)

    $( Absorption of an incomparable atom. $)
    atabs2i $p |- ( C e. HAtoms -> ( -. C C_ ( A vH B ) ->
                ( ( A vH C ) i^i ( A vH B ) ) = A ) ) $=
      ( cat wcel chj co wss wn cin chjcli atabsi chjassi chjidmi opreq1i eqtr3i
      wceq sseq2i notbii chabs2i eqeq2i 3imtr3g ) CFGCAABHIZHIZJZKACHIUELZAUELZ
      SCUEJZKUHASAUECDABDEMNUGUJUFUECAAHIZBHIUFUEAABDDEOUKABHADPQRTUAUIAUHABDEU
      BUCUD $.
      $( [19-Feb-2006] $) $( [18-Jul-2004] $)
  $}

