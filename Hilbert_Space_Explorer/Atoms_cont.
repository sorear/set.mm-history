$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Atoms (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    atcvat3.1 $e |- A e. CH $.
    $( A condition implying that a certain lattice element is an atom.  Part
       of Lemma 3.2.20 of [PtakPulmannova] p. 68. $)
    atcvat3 $p |- ( ( B e. Atoms /\ C e. Atoms ) ->
          ( ( ( -. B = C /\ -. C (_ A ) /\ B (_ ( A vH C ) ) ->
          ( A i^i ( B vH C ) ) e. Atoms ) ) $=
      ( cat wcel wa wceq wn wss chj co cin ccv wbr wi cch wb chcv1t mpan
      biimpa ad2ant2lr chjcomt opreq2d chjasst mp3an1 ancoms eqtr4d adantr w3a
      chlej2t ex pm3.26 chjclt adantl syl3anc imp eqsstrd chjidmt syl ad2antlr
      sseqtrd mp3anl3 pm3.27 jca chub2t sylanc eqssd atelch anim12i sylan
      breq2d adantrl mpbird jctil syl2an cvexcht sylibrd atcvat2t chinclt
      exp3a syld exp4b imp4c ) BEFZCEFZGZBCHIZCAJIZBACKLZJZABCKLZMZEFZWGWHWIWKW
      NWGWHGWIWKGZWMWLNOZWNWGWOWPPWHWGWOAAWLKLZNOZWPWGWOWRWGWOGWRAWJNOZWFWIWSWE
      WKWFWIWSAQFZWFWIWSRDACSTUAUBWGWKWRWSRWIWGWKGWQWJANBQFZCQFZGZWKWQWJHWGXCWK
      GZWQWJXDWQWJWJKLZWJXDWQWJBKLZXEXCWQXFHWKXCWQACBKLZKLZXFXCWLXGAKBCUCUDXBXA
      XFXHHZWTXBXAXIDACBUEUFUGUHUIXCWKXFXEJZXAWJQFZXKWKXJPXCXAXKXKUJWKXJBWJWJUK
      ULXAXBUMXBXKXAWTXBXKDACUNTZUOZXMUPUQURXBXEWJHZXAWKXBXKXNXLWJUSUTVAVBXCWJW
      QJZWKXBWLQFZGCWLJZXOXCXBXPWTXQXODCWLAUKVCXCXBXPXAXBVDBCUNZVEXBXAXQCBVFUGV
      GUIVHWEXAWFXBBVIZCVIZVJVKVLVMVNULWGWTXPGZWPWRRXAXBYAWEWFXCXPWTXRDVOZXSXTV
      PAWLVQUTVRUIWGWHWPWNPWGWHWPWNWMQFZWEWFWHWPGWNPWGWMBCVSXAXBYCWEWFXCYAYCYBA
      WLVTUTXSXTVPWEWFUMWEWFVDUPWAUQWBWCWD $.
      $( [29-Jul-2004] $) $( [2-Jul-2004] $)

    $( A condition implying existence of an atom with the properties shown.
       Lemma 3.2.20 of [PtakPulmannova] p. 68. $)
    atcvat4 $p |- ( ( B e. Atoms /\ C e. Atoms ) ->
          ( ( A =/= 0H /\ B (_ ( A vH C ) ) ->
          E. x e. Atoms ( x (_ A /\ B (_ ( C vH x ) ) ) ) $=
      ( cat wcel wa wceq wss c0h wne chj co cv wrex wi sseq1 cch chub1t atelch
      syl2an syl5bir exp3a impcom anim2d com23 r19.22dv hatomic syl5 ex a1i
      com4l imp4a adantl wb chlejb2t mpan2 biimpa sseq2d anasss chub2t jctird
      pm3.26 jctild imp anassrs opreq2 anbi12d rcla4ev syl adantrl exp31 wo wn
      cin atcvat3 w3a atexcht ad2antlr simpll 3jca inss2 chjcomt sseqtrd
      adantr atnssm0 mpan chinclt pm3.27 chjclt sylanc chle0t inss1 sslin
      ax-mp incom sseqtr sseq2 mpbii syl5bi sylbid adantrr jca sylc jctil jcad
      syl6 ioran syl5ib syl7 ecase3d ) CFGZDFGZHZCDIZDBJZBKLZCBDMNZJZHZAOZBJZCD
      YBMNZJZHZAFPZQZXNXPYHQXMXNXPXRXTYGXTXNXPXRYGXNXPXRYGQZQQXTXNXPYIXNXPHZYCA
      FPYGXRYJYCYFAFYJYCYBFGZYFYJYCYKYFYJYKYEYCXPXNYKYEQXPXNYKYEXPYEDYDJZXNYKHC
      DYDRDSGZYBSGYLXNYKDYBTDUAZYBUAUBUCUDUEUFUDUGUHABEUIUJUKULUMUNUOXOXQYAYGXO
      XQHZXTYGXRYOXTHXMCBJZCDCMNZJZHZHZYGXOXQXTYTXOXQXTHZYTXOUUAYSXMCSGZYMUUAYS
      QXMXNUUBYMHZUUAYPYRYMUUAYPQUUBYMUUAYPYMXQXTYPYMXQHZXTYPUUDXSBCYMXQXSBIZYM
      BSGZXQUUEUPEDBUQURUSUTUSVAUKUOCDVBVCCUAZYNUBXMXNVDVEVFVGYFYSACFYBCIZYCYPY
      EYRYBCBRUUHYDYQCYBCDMVHUTVIVJVKVLVMXOXPXQVNVOZXTYGYAXOXPVOZXQVOZHZXTYGQUU
      IXOUULXTYGXOUULXTHZBCDMNZVPZFGZUUOBJZCDUUOMNZJZHZHYGXOUUMUUPUUTBCDEVQZXOU
      UMUUTXOUUMHZUUSUUQYMUUPXMVRUUOYQJZDUUOVPZKIZHUUSUVBDUUOCVSUVBYMUUPXMXNYMX
      MUUMYNVTXOUUMUUPUVAVFXMXNUUMWAWBUVBUVCUVEXOUVCUUMXOUUOUUNYQUUOUUNJXOBUUNW
      CULUUBYMUUNYQIXMXNCDWDUUGYNUBWEWFXOUULUVEXTXOUUKUVEUUJXOUUKUVEXOUUKBDVPZK
      IZUVEXNUUKUVGUPZXMUUFXNUVHEBDWGWHUOXOUVDKJZUVEUVGXOUVDSGZUVIUVEUPUUBYMUVJ
      XMXNYMUUOSGZUVJUUCDUUOWIUUBYMWJUUCUUNSGZUVKCDWKUUFUVLUVKEBUUNWIWHVKWLUUGY
      NUBUVDWMVKUVGUVDUVFJUVIUVDDBVPZUVFUUQUVDUVMJBUUNWNZUUOBDWOWPDBWQWRUVFKUVD
      WSWTXAXBVFVLXCXDXEUVNXFUKXGYFUUTAUUOFYBUUOIZYCUUQYEUUSYBUUOBRUVOYDUURCYBU
      UODMVHUTVIVJXHUDXPXQXIXJXRXTWJXKXL $.
      $( [22-Apr-2007] $) $( [2-Jul-2004] $)
  $}

  $( Two Hilbert lattice elements have the dual modular pair property if the
     first is an atom.  Theorem 7.6(c) of [MaedaMaeda] p. 31. $)
  atdmd $p |- ( ( A e. Atoms /\ B e. CH ) -> A MH* B ) $=
    ( cat wcel cch wa cin c0h wceq cdmd wbr chj co ccv wb cvp chjcomt atelch
    sylan2 breq2d bitrd ancoms wi cvdmdt 3expia sylan sylbid wn wss atnssm0
    con1bid ssdmd1 pm2.61d ) ACDZBEDZFZBAGHIZABJKZUPUQBABLMZNKZURUOUNUQUTOUOUNF
    ZUQBBALMZNKUTBAPVAVBUSBNUOAEDZVBUSIUNBAQARZSTUAUBVCUOUTURUCUNVCUOUTURABUDUE
    VDUFUGUPUQUHABUIZURUPVEUQUOUNVEUHUQOBAUJUBUKVCUOVEURUCUNVCUOVEURABULUEVDUFU
    GUM $.
    $( [27-Jul-2007] $) $( [22-Jun-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Two Hilbert lattice elements have the modular pair property if the
       first is an atom.  Theorem 7.6(b) of [MaedaMaeda] p. 31. $)
    atmd $p |- ( ( A e. Atoms /\ B e. CH ) -> A MH B ) $=
      ( vx cat wcel cch wa cv cmd wbr wral cdmd atdmd r19.21aiva wb atelch
      mddmd syl mpbird adantr wi breq2 rcla4v adantl mpd ) ADEZBFEZGACHZIJZCFKZ
      ABIJZUFUJUGUFUJAUHLJZCFKZUFULCFAUHMNUFAFEUJUMOAPCAQRSTUGUJUKUAUFUIUKCBFUH
      BAIUBUCUDUE $.
      $( [12-Aug-2005] $) $( [22-Jun-2004] $)
  $}

  $( Two Hilbert lattice elements have the dual modular pair property if the
     second is an atom.  Part of Exercise 6 of [Kalmbach] p. 103. $)
  atmd2 $p |- ( ( A e. CH /\ B e. Atoms ) -> A MH B ) $=
    ( cch wcel cat wa cin c0h wceq cmd wbr chj co ccv cvp wi cvexcht cvmdt
    3expia sylbird atelch sylan2 sylbid wn wss atnssm0 con1bid ssmd2 3com12
    syl3an2 pm2.61d ) ACDZBEDZFZABGZHIZABJKZUNUPAABLMNKZUQABOULBCDZURUQPUMULUSF
    URUOBNKZUQABQULUSUTUQABRSTBUAZUBUCUNUPUDBAUEZUQUNVBUPABUFUGULUMVBUQULUSVBUQ
    UMUSULVBUQBAUHUIVAUJSUCUK $.
    $( [23-Sep-2005] $) $( [22-Jun-2004] $)

  ${
    atabs.1 $e |- A e. CH $.
    atabs.2 $e |- B e. CH $.
    $( Absorption of an incomparable atom.  Similar to Exercise 7.1 of
       [MaedaMaeda] p. 34. $)
    atabs $p |- ( C e. Atoms -> ( -. C (_ ( A vH B ) ->
                ( ( A vH C ) i^i B ) = ( A i^i B ) ) ) $=
      ( cat wcel chj co wss wn cin wceq wa chub1 cch cmd wbr wi chjcl w3a mdit
      exp32 mp3an23 atelch atmd mpan2 sylc mpi adantr c0h wb atnssm0 mpan
      biimpa incom syl5eq opreq2d chj0 syl6eq eqtrd ineq1d inass chjcom ineq1i
      chabs2 3eqtr ineq2i eqtr2 ex ) CFGZCABHIZJKZACHIZBLZABLZMVKVMNZVNVLLZBLZV
      PVOVQVRABVQVRACVLLZHIZAVKVRWAMZVMVKAVLJZWBABDEOCPGZCVLQRZWCWBSZVKWDVLPGZA
      PGZWEWFSABDETZDWDWGWHUAWEWCWBCVLAUBUCUDCUEVKWGWEWICVLUFUGUHUIUJVQWAAUKHIA
      VQVTUKAHVQVLCLZUKVTVKVMWJUKMZWGVKVMWKULWIVLCUMUNUOCVLUPUQURADUSUTVAVBVSVN
      VLBLZLVOVNVLBVCWLBVNWLBAHIZBLBWMLBVLWMBABDEVDVEWMBUPBAEDVFVGVHVIUQVJ $.
      $( [29-Dec-2005] $) $( [15-Jul-2004] $)

    $( Absorption of an incomparable atom. $)
    atabs2 $p |- ( C e. Atoms -> ( -. C (_ ( A vH B ) ->
                ( ( A vH C ) i^i ( A vH B ) ) = A ) ) $=
      ( cat wcel chj co wss wn cin wceq chjcl atabs chjass chjidm opreq1i
      eqtr3 sseq2i negbii chabs2 eqeq2i 3imtr3g ) CFGCAABHIZHIZJZKACHIUELZAUELZ
      MCUEJZKUHAMAUECDABDENOUGUJUFUECAAHIZBHIUFUEAABDDEPUKABHADQRSTUAUIAUHABDEU
      BUCUD $.
      $( [19-Feb-2006] $) $( [18-Jul-2004] $)
  $}

