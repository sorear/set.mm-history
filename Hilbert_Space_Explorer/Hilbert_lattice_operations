$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Hilbert lattice operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The zero subspace is the smallest subspace. $)
  sh0le $p |- ( A e. SH -> 0H C_ A ) $=
    ( csh wcel c0v csn c0h sh0 snssd df-ch0 syl5ss ) ABCZDEAFKDAAGHIJ $.
    $( [4-Jun-2004] $) $( [3-Jun-2004] $)

  $( The zero subspace is the smallest member of ` CH ` . $)
  ch0le $p |- ( A e. CH -> 0H C_ A ) $=
    ( cch wcel csh c0h wss chsh sh0le syl ) ABCADCEAFAGAHI $.
    $( [14-Aug-2002] $)

  $( No subspace is smaller than the zero subspace. $)
  shle0 $p |- ( A e. SH -> ( A C_ 0H <-> A = 0H ) ) $=
    ( csh wcel c0h wss wa wceq sh0le biantrud eqss syl6bbr ) ABCZADEZMDAEZFADGL
    NMAHIADJK $.
    $( [26-Nov-2004] $) $( [24-Nov-2004] $)

  $( No Hilbert lattice element is smaller than zero. $)
  chle0 $p |- ( A e. CH -> ( A C_ 0H <-> A = 0H ) ) $=
    ( cch wcel csh c0h wss wceq wb chsh shle0 syl ) ABCADCAEFAEGHAIAJK $.
    $( [14-Aug-2002] $)

  $( A Hilbert lattice element that is not a subset of another is nonzero. $)
  chnlen0 $p |- ( B e. CH -> ( -. A C_ B -> -. A = 0H ) ) $=
    ( cch wcel c0h wceq wss sseq1 ch0le syl5cbir con3d ) BCDZAEFZABGZMNEBGLAEBH
    BIJK $.
    $( [2-Jul-2004] $) $( [30-Jun-2004] $)

  $( The zero subspace is a proper subset of nonzero Hilbert lattice
     elements. $)
  ch0pss $p |- ( A e. CH -> ( 0H C. A <-> A =/= 0H ) ) $=
    ( cch wcel c0h wne wss wa wpss ch0le biantrurd necom syl5bbr df-pss
    syl6rbbr ) ABCZADEZDAFZDAEZGZDAHORSPOQRAIJDAKLDAMN $.
    $( [18-Jun-2006] $) $( [9-Jun-2004] $)

  $( The intersection of orthogonal subspaces is the zero subspace. $)
  orthin $p |- ( ( A e. SH /\ B e. SH ) ->
               ( A C_ ( _|_ ` B ) -> ( A i^i B ) = 0H ) ) $=
    ( csh wcel wa cort cfv wss cin c0h wceq wi ocin sseq2d ssrin incom syl6ss
    syl5bi adantl shincl sh0le syl jctird eqss syl6ibr ) ACDZBCDZEZABFGZHZABIZJ
    HZJUKHZEUKJKUHUJULUMUGUJULLUFUGUKBUIIZHULUJUGUNJUKBMNUJUKUIBIUNAUIBOUIBPQRS
    UHUKCDUMABTUKUAUBUCUKJUDUE $.
    $( [24-Jun-2004] $) $( [24-Jun-2004] $)

  ${
    $d x y A $.
    shne0.1 $e |- A e. SH $.
    $( A nonzero subspace has a nonzero vector. $)
    shne0i $p |- ( A =/= 0H <-> E. x e. A x =/= 0h ) $=
      ( c0h wne wceq wn cv wcel wrex c0v df-ne wa wex wss df-rex nss csh wb
      shle0 ax-mp notbii 3bitr2ri elch0 necon3bbii rexbii 3bitri ) BDEBDFZGZAHZ
      DIZGZABJZUJKEZABJBDLUMUJBIULMANBDOZGUIULABPABDQUOUHBRIUOUHSCBTUAUBUCULUNA
      BUKUJKUJUDUEUFUG $.
      $( [18-Mar-2006] $) $( [25-Feb-2006] $)

    $( Hilbert subspace sum with the zero subspace. $)
    shs0i $p |- ( A +H 0H ) = A $=
      ( c0h cph co cun cspn cfv h0elsh shsumval3i wss wceq csh wcel sh0le
      ax-mp ssequn2 mpbi fveq2i spanid 3eqtri ) ACDEACFZGHAGHZAACBIJUBAGCAKZUBA
      LAMNZUDBAOPCAQRSUEUCALBATPUA $.
      $( [17-Jan-2005] $) $( [14-Jan-2005] $)

    ${
      shs00.2 $e |- B e. SH $.
      $( Two subspaces are zero iff their join is zero. $)
      shs00i $p |- ( ( A = 0H /\ B = 0H ) <-> ( A +H B ) = 0H ) $=
        ( c0h wceq wa cph co opreq12 h0elsh shs0i syl6eq wss shsub1i sseq2
        mpbii csh wcel wb shle0 ax-mp sylib shsub2i jca impbii ) AEFZBEFZGZABHI
        ZEFZUIUJEEHIEAEBEHJEKLMUKUGUHUKAENZUGUKAUJNULABCDOUJEAPQARSULUGTCAUAUBU
        CUKBENZUHUKBUJNUMBADCUDUJEBPQBRSUMUHTDBUAUBUCUEUF $.
        $( [11-Aug-2004] $) $( [7-Aug-2004] $)
    $}
  $}

  ${
    $d x y A $.
    ch0le.1 $e |- A e. CH $.
    $( The closed subspace zero is the smallest member of ` CH ` . $)
    ch0lei $p |- 0H C_ A $=
      ( cch wcel c0h wss ch0le ax-mp ) ACDEAFBAGH $.
      $( [15-Oct-1999] $)

    $( No Hilbert closed subspace is smaller than zero. $)
    chle0i $p |- ( A C_ 0H <-> A = 0H ) $=
      ( cch wcel c0h wss wceq wb chle0 ax-mp ) ACDAEFAEGHBAIJ $.
      $( [7-Apr-2001] $)

    $( A nonzero closed subspace has a nonzero vector. $)
    chne0i $p |- ( A =/= 0H <-> E. x e. A x =/= 0h ) $=
      ( chshii shne0i ) ABBCDE $.
      $( [19-Mar-2006] $) $( [25-Feb-2006] $)

    $( Intersection of a closed subspace and its orthocomplement.  Part of
       Proposition 1 of [Kalmbach] p. 65. $)
    chocini $p |- ( A i^i ( _|_ ` A ) ) = 0H $=
      ( csh wcel cort cfv cin c0h wceq chshii ocin ax-mp ) ACDAAEFGHIABJAKL $.
      $( [6-Aug-2004] $) $( [11-Oct-1999] $)

    $( Join with lattice zero in ` CH ` . $)
    chj0i $p |- ( A vH 0H ) = A $=
      ( c0h chj co cun cort cfv h0elch chjvali wss wceq ch0lei ssequn2 mpbi
      fveq2i pjococi 3eqtri ) ACDEACFZGHZGHAGHZGHAACBIJTUAGSAGCAKSALABMCANOPPAB
      QR $.
      $( [15-Oct-1999] $)

    $( Meet with lattice one in ` CH ` . $)
    chm1i $p |- ( A i^i ~H ) = A $=
      ( chil cin inss1 ssid chssii ssini eqssi ) ACDAACEAACAFABGHI $.
      $( [24-Oct-1999] $)

    ${
      $d x y B $.  $d x y C $.
      chjcl.2 $e |- B e. CH $.
      $( Closure of ` CH ` join. $)
      chjcli $p |- ( A vH B ) e. CH $=
        ( chshii shjcli ) ABACEBDEF $.
        $( [29-Jul-1999] $)

      $( Subspace sum is smaller than subspace join.  Remark in [Kalmbach]
         p. 65. $)
      chsleji $p |- ( A +H B ) C_ ( A vH B ) $=
        ( chshii shsleji ) ABACEBDEF $.
        $( [17-Oct-1999] $)

      $( Membership in subspace sum. $)
      chseli $p |- ( C e. ( A +H B )
           <-> E. x e. A E. y e. B C = ( x +h y ) ) $=
        ( chshii shseli ) ABCDECFHDGHI $.
        $( [19-Oct-1999] $)

      $( Closure of Hilbert lattice intersection. $)
      chincli $p |- ( A i^i B ) e. CH $=
        ( cpr cint cin cch elisseti intpr wss c0 wne wcel wa pm3.2i prss mpbi
        prnz chintcli eqeltrri ) ABEZFABGHABAHCIZBHDIZJUBUBHKZUBLMAHNZBHNZOUEUF
        UGCDPABHUCUDQRABUCSPTUA $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chsscon3i $p |- ( A C_ B <-> ( _|_ ` B ) C_ ( _|_ ` A ) ) $=
        ( wss cort cfv chil wi chssii occon mp2an choccli pjococi 3sstr3g
        impbii ) ABEZBFGZAFGZEZAHEBHEQTIACJBDJABKLTSFGZRFGZABRHESHETUAUBEIRBDMJ
        SACMJRSKLACNBDNOP $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chsscon1i $p |- ( ( _|_ ` A ) C_ B <-> ( _|_ ` B ) C_ A ) $=
        ( cort cfv wss choccli chsscon3i pjococi sseq2i bitri ) AEFZBGBEFZMEFZG
        NAGMBACHDIOANACJKL $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chsscon2i $p |- ( A C_ ( _|_ ` B ) <-> B C_ ( _|_ ` A ) ) $=
        ( cort cfv wss choccli chsscon3i pjococi sseq1i bitri ) ABEFZGMEFZAEFZG
        BOGAMCBDHINBOBDJKL $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chcon2i $p |- ( A = ( _|_ ` B ) <-> B = ( _|_ ` A ) ) $=
        ( cort cfv wss wa wceq chsscon2i chsscon1i anbi12i eqss 3bitr4i ) ABEFZ
        GZOAGZHBAEFZGZRBGZHAOIBRIPSQTABCDJBADCKLAOMBRMN $.
        $( [24-Jun-2004] $) $( [24-Jun-2004] $)

      $( Hilbert lattice contraposition law. $)
      chcon1i $p |- ( ( _|_ ` A ) = B <-> ( _|_ ` B ) = A ) $=
        ( cort cfv wceq chcon2i eqcom 3bitr4i ) BAEFZGABEFZGKBGLAGBADCHKBILAIJ
        $.
        $( [3-Oct-2006] $) $( [15-Jun-2006] $)

      $( Hilbert lattice contraposition law. $)
      chcon3i $p |- ( A = B <-> ( _|_ ` B ) = ( _|_ ` A ) ) $=
        ( wss wa cort cfv wceq chsscon3i anbi12i eqss 3bitr4i ) ABEZBAEZFBGHZAG
        HZEZQPEZFABIPQINROSABCDJBADCJKABLPQLM $.
        $( [25-Jun-2004] $) $( [24-Jun-2004] $)

      $( Union is smaller than ` CH ` join. $)
      chunssji $p |- ( A u. B ) C_ ( A vH B ) $=
        ( chshii shunssji ) ABACEBDEF $.
        $( [15-Oct-1999] $)

      $( Commutative law for join in ` CH ` . $)
      chjcomi $p |- ( A vH B ) = ( B vH A ) $=
        ( chshii shjcomi ) ABACEBDEF $.
        $( [14-Oct-1999] $)

      $( ` CH ` join is an upper bound of two elements. $)
      chub1i $p |- A C_ ( A vH B ) $=
        ( chshii shub1i ) ABACEBDEF $.
        $( [15-Oct-1999] $)

      $( ` CH ` join is an upper bound of two elements. $)
      chub2i $p |- A C_ ( B vH A ) $=
        ( chj co chub1i chjcomi sseqtri ) AABEFBAEFABCDGABCDHI $.
        $( [5-Nov-2000] $)

      ${
        $d x y C $.
        chlub.1 $e |- C e. CH $.
        $( Hilbert lattice join is the least upper bound of two elements. $)
        chlubi $p |- ( ( A C_ C /\ B C_ C ) <-> ( A vH B ) C_ C ) $=
          ( chshii shlubi ) ABCADGBEGFH $.
          $( [16-Jun-2004] $) $( [11-Jun-2004] $)

        $( Hilbert lattice join is the least upper bound of two elements (one
           direction of ~ chlubi ). $)
        chlubii $p |- ( ( A C_ C /\ B C_ C ) -> ( A vH B ) C_ C ) $=
          ( wss wa chj co chlubi biimpi ) ACGBCGHABIJCGABCDEFKL $.
          $( [15-Oct-1999] $)

        $( Add join to both sides of a Hilbert lattice ordering. $)
        chlej1i $p |- ( A C_ B -> ( A vH C ) C_ ( B vH C ) ) $=
          ( chshii shlej1i ) ABCADGBEGCFGH $.
          $( [19-Oct-1999] $)

        $( Add join to both sides of a Hilbert lattice ordering. $)
        chlej2i $p |- ( A C_ B -> ( C vH A ) C_ ( C vH B ) ) $=
          ( chshii shlej2i ) ABCADGBEGCFGH $.
          $( [19-Oct-1999] $)

        ${
          chlej12.4 $e |- D e. CH $.
          $( Add join to both sides of a Hilbert lattice ordering. $)
          chlej12i $p |- ( ( A C_ B /\ C C_ D ) ->
                       ( A vH C ) C_ ( B vH D ) ) $=
            ( wss chj co chlej1i chlej2i sylan9ss ) ABICDIACJKBCJKBDJKABCEFGLCD
            BGHFMN $.
            $( [19-Oct-1999] $)
        $}
      $}

      $( Hilbert lattice ordering in terms of join. $)
      chlejb1i $p |- ( A C_ B <-> ( A vH B ) = B ) $=
        ( wss chj co wceq wa ssid chlubii mpan2 chub2i jctir eqss sylibr wi
        chub1i sstr2 eqimss syl5 ax-mp impbii ) ABEZABFGZBHZUDUEBEZBUEEZIUFUDUG
        UHUDBBEUGBJABBCDDKLBADCMNUEBOPAUEEZUFUDQABCDRUIUGUDUFAUEBSUEBTUAUBUC $.
        $( [15-Oct-1999] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm1i $p |- ( _|_ ` ( A i^i B ) ) = ( ( _|_ ` A ) vH ( _|_ ` B ) ) $=
        ( cin cort cfv chj co wss choccli chub1i chjcli chsscon1i mpbi chub2i
        ssini chincli inss1 chsscon3i inss2 chlubii mp2an eqssi ) ABEZFGZAFGZBF
        GZHIZUIFGZUEJUFUIJUJABUGUIJUJAJUGUHACKZBDKZLAUICUGUHUKULMZNOUHUIJUJBJUH
        UGULUKPBUIDUMNOQUIUEUMABCDRZNOUGUFJZUHUFJZUIUFJUEAJUOABSUEAUNCTOUEBJUPA
        BUAUEBUNDTOUGUHUFUKULUEUNKUBUCUD $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm2i $p |- ( _|_ ` ( ( _|_ ` A ) i^i B ) ) = ( A vH ( _|_ ` B ) ) $=
        ( cort cfv cin chj co choccli chdmm1i pjococi opreq1i eqtri ) AEFZBGEFO
        EFZBEFZHIAQHIOBACJDKPAQHACLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm3i $p |- ( _|_ ` ( A i^i ( _|_ ` B ) ) ) = ( ( _|_ ` A ) vH B ) $=
        ( cort cfv cin chj co choccli chdmm1i pjococi opreq2i eqtri ) ABEFZGEFA
        EFZOEFZHIPBHIAOCBDJKQBPHBDLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm4i $p |- ( _|_ ` ( ( _|_ ` A ) i^i ( _|_ ` B ) ) ) = ( A vH B ) $=
        ( cort cfv cin chj co choccli chdmm2i pjococi opreq2i eqtri ) AEFBEFZGE
        FAOEFZHIABHIAOCBDJKPBAHBDLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj1i $p |- ( _|_ ` ( A vH B ) ) = ( ( _|_ ` A ) i^i ( _|_ ` B ) ) $=
        ( cort cfv cin chj co chdmm4i fveq2i choccli chincli pjococi eqtr3i )
        AEFZBEFZGZEFZEFABHIZEFRSTEABCDJKRPQACLBDLMNO $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj2i $p |- ( _|_ ` ( ( _|_ ` A ) vH B ) ) = ( A i^i ( _|_ ` B ) ) $=
        ( cort cfv chj co cin choccli chdmj1i pjococi ineq1i eqtri ) AEFZBGHEFO
        EFZBEFZIAQIOBACJDKPAQACLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj3i $p |- ( _|_ ` ( A vH ( _|_ ` B ) ) ) = ( ( _|_ ` A ) i^i B ) $=
        ( cort cfv chj co cin choccli chdmj1i pjococi ineq2i eqtri ) ABEFZGHEFA
        EFZOEFZIPBIAOCBDJKQBPBDLMN $.
        $( [23-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj4i $p |- ( _|_ ` ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) = ( A i^i B ) $=
        ( cort cfv chj co cin choccli chdmj2i pjococi ineq2i eqtri ) AEFBEFZGHE
        FAOEFZIABIAOCBDJKPBABDLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( Equivalent expressions for "not less than" in the Hilbert lattice. $)
      chnlei $p |- ( -. B C_ A <-> A C. ( A vH B ) ) $=
        ( chj co wceq wn wss wa wpss chub1i biantrur chlejb1i eqcom chjcomi
        eqeq2i 3bitri notbii dfpss2 3bitr4i ) AABEFZGZHZAUBIZUDJBAIZHAUBKUEUDAB
        CDLMUFUCUFBAEFZAGAUGGUCBADCNUGAOUGUBABADCPQRSAUBTUA $.
        $( [16-Jun-2004] $) $( [5-Jun-2004] $)

      ${
        chjass.3 $e |- C e. CH $.
        $( Associative law for Hilbert lattice join.  From definition of
           lattice in [Kalmbach] p. 14. $)
        chjassi $p |- ( ( A vH B ) vH C ) = ( A vH ( B vH C ) ) $=
          ( chj co cort cfv cin inass chdmj1i ineq1i ineq2i 3eqtr4i fveq2i
          chjcli chdmm4i 3eqtr3i ) ABGHZIJZCIJZKZIJAIJZBCGHZIJZKZIJUACGHAUFGHUD
          UHIUEBIJZKZUCKUEUIUCKZKUDUHUEUIUCLUBUJUCABDEMNUGUKUEBCEFMOPQUACABDERF
          SAUFDBCEFRST $.
          $( [17-Jun-2004] $) $( [10-Jun-2004] $)
      $}

      $( Two Hilbert lattice elements are zero iff their join is zero. $)
      chj00i $p |- ( ( A = 0H /\ B = 0H ) <-> ( A vH B ) = 0H ) $=
        ( c0h wceq wa chj co opreq12 h0elch chj0i syl6eq wss chub1i sseq2
        mpbii chle0i sylib chub2i jca impbii ) AEFZBEFZGZABHIZEFZUEUFEEHIEAEBEH
        JEKLMUGUCUDUGAENZUCUGAUFNUHABCDOUFEAPQACRSUGBENZUDUGBUFNUIBADCTUFEBPQBD
        RSUAUB $.
        $( [11-Aug-2004] $) $( [7-Aug-2004] $)
    $}

    $( The join of a closed subspace and its orthocomplement. $)
    chjoi $p |- ( A vH ( _|_ ` A ) ) = ~H $=
      ( cort cfv cin c0h chj co chil incom chocini eqtri fveq2i chdmm2i choc0
      3eqtr3i ) ACDZAEZCDFCDAQGHIRFCRAQEFQAJABKLMAABBNOP $.
      $( [24-Oct-1999] $)

    $( Join with Hilbert lattice unit. $)
    chj1i $p |- ( A vH ~H ) = ~H $=
      ( chil chj co helch chjcli chssii chub2i eqssi ) ACDEZCKACBFGHCAFBIJ $.
      $( [11-Aug-2004] $) $( [6-Aug-2004] $)

    $( Meet with Hilbert lattice zero. $)
    chm0i $p |- ( A i^i 0H ) = 0H $=
      ( c0h cin inss2 ch0lei ssid ssini eqssi ) ACDCACECACABFCGHI $.
      $( [11-Aug-2004] $) $( [6-Aug-2004] $)

  $}

  $( Meet with Hilbert lattice zero. $)
  chm0 $p |- ( A e. CH -> ( A i^i 0H ) = 0H ) $=
    ( cch wcel c0h cin wceq cif ineq1 eqeq1d h0elch elimel chm0i dedth ) ABCZAD
    EZDFNADGZDEZDFADAPFOQDAPDHIPADBJKLM $.
    $( [14-Jun-2006] $) $( [14-Jun-2006] $)

  ${
    shjshs.1 $e |- A e. SH $.
    shjshs.2 $e |- B e. SH $.
    $( Hilbert lattice join equals the double orthocomplement of subspace
       sum. $)
    shjshsi $p |- ( A vH B ) = ( _|_ ` ( _|_ ` ( A +H B ) ) ) $=
      ( chj co cph cort cfv cun csh wcel wceq shjval mp2an wss shunssi chil
      shssii unssi shscli occon2i ax-mp eqsstri shsleji wi shjcli chssii occon
      occli chsscon1i mpbi eqssi ) ABEFZABGFZHIZHIZUNABJZHIHIZUQAKLBKLUNUSMCDAB
      NOURUOPUSUQPABCDQURUOABRACSBDSTUOABCDUASZUBUCUDUNHIUPPZUQUNPUOUNPZVAABCDU
      EUORPUNRPVBVAUFUTUNABCDUGZUHUOUNUIOUCUNUPVCUOUTUJUKULUM $.
      $( [1-Dec-2004] $) $( [27-Nov-2004] $)

    $( A closed subspace sum equals Hilbert lattice join.  Part of Lemma 31.1.5
       of [MaedaMaeda] p. 136. $)
    shjshseli $p |- ( ( A +H B ) e. CH <-> ( A +H B ) = ( A vH B ) ) $=
      ( cph co cch wcel chj wceq cort cfv ococ shjshsi syl5req shjcli eleq1
      mpbiri impbii ) ABEFZGHZTABIFZJZUATKLKLTUBTMABCDNOUCUAUBGHABCDPTUBGQRS $.
      $( [3-Jan-2005] $) $( [30-Nov-2004] $)
  $}

  ${
    $d x A $.
    $( A nonzero closed subspace has a nonzero vector. $)
    chne0 $p |- ( A e. CH -> ( A =/= 0H <-> E. x e. A x =/= 0h ) ) $=
      ( cch wcel c0h wne cv c0v wrex wb cif wceq neeq1 rexeq1 bibi12d h0elch
      elimel chne0i dedth ) BCDZBEFZAGHFZABIZJTBEKZEFZUBAUDIZJBEBUDLUAUEUCUFBUD
      EMUBABUDNOAUDBECPQRS $.
      $( [19-Jun-2006] $) $( [25-Feb-2006] $)
  $}

  $( Intersection of a closed subspace and its orthocomplement.  Part of
     Proposition 1 of [Kalmbach] p. 65. $)
  chocin $p |- ( A e. CH -> ( A i^i ( _|_ ` A ) ) = 0H ) $=
    ( cch wcel cort cfv cin c0h wceq cif id fveq2 ineq12d eqeq1d h0elch elimel
    chocini dedth ) ABCZAADEZFZGHRAGIZUADEZFZGHAGAUAHZTUCGUDAUASUBUDJAUADKLMUAA
    GBNOPQ $.
    $( [14-Jun-2006] $) $( [13-Jun-2006] $)

  $( A closed subspace less than its orthocomplement is zero. $)
  chssoc $p |- ( A e. CH -> ( A C_ ( _|_ ` A ) <-> A = 0H ) ) $=
    ( cch wcel cort cfv wss c0h wceq cin chocin sseq2d chle0 bitrd sslin inidm
    syl5ssr syl5bi wa pm3.27 choccl ch0le syl adantr eqsstrd ex impbid ) ABCZAA
    DEZFZAGHZUGAAUHIZFZUJUIUGULAGFUJUGUKGAAJKALMUIAAIUKAAUHANAOPQUGUJUIUGUJRAGU
    HUGUJSUGGUHFZUJUGUHBCUMATUHUAUBUCUDUEUF $.
    $( [27-Jun-2006] $) $( [14-Jun-2006] $)

  $( Join with Hilbert lattice zero. $)
  chj0 $p |- ( A e. CH -> ( A vH 0H ) = A ) $=
    ( cch wcel c0h chj co wceq cif opreq1 id eqeq12d h0elch elimel chj0i dedth
    ) ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [25-Jun-2004] $) $( [22-Jun-2004] $)

  $( Subspace sum is smaller than subspace join.  Remark in [Kalmbach]
     p. 65. $)
  chslej $p |- ( ( A e. CH /\ B e. CH ) -> ( A +H B ) C_ ( A vH B ) ) $=
    ( csh wcel cph co chj wss cch shslej chsh syl2an ) ACDBCDABEFABGFHAIDBIDABJ
    AKBKL $.
    $( [2-Sep-2004] $) $( [12-Jul-2004] $)

  $( Closure of Hilbert lattice intersection. $)
  chincl $p |- ( ( A e. CH /\ B e. CH ) -> ( A i^i B ) e. CH ) $=
    ( cch wcel cin chil cif wceq ineq1 eleq1d ineq2 helch elimel chincli
    dedth2h ) ACDZBCDZABEZCDPAFGZBEZCDSQBFGZEZCDABFFASHRTCASBIJBUAHTUBCBUASKJSU
    AAFCLMBFCLMNO $.
    $( [20-Jun-2004] $) $( [15-Jun-2004] $)

  $( Hilbert lattice contraposition law. $)
  chsscon3 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A C_ B <-> ( _|_ ` B ) C_ ( _|_ ` A ) ) ) $=
    ( cch wcel wss cort cfv wb chil cif wceq sseq1 fveq2 sseq2d bibi12d sseq2
    sseq1d helch elimel chsscon3i dedth2h ) ACDZBCDZABEZBFGZAFGZEZHUBAIJZBEZUEU
    HFGZEZHUHUCBIJZEZULFGZUJEZHABIIAUHKZUDUIUGUKAUHBLUPUFUJUEAUHFMNOBULKZUIUMUK
    UOBULUHPUQUEUNUJBULFMQOUHULAICRSBICRSTUA $.
    $( [20-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice contraposition law. $)
  chsscon1 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( ( _|_ ` A ) C_ B <-> ( _|_ ` B ) C_ A ) ) $=
    ( cch wcel wa cort cfv wss wb chsscon3 choccl sylan wceq ococ adantr
    sseq2d bitrd ) ACDZBCDZEZAFGZBHZBFGZUAFGZHZUCAHUACDSUBUEIRUABJAKLTUDAUCRUDA
    MSANOPQ $.
    $( [25-Jun-2004] $) $( [21-Jun-2004] $)

  $( Hilbert lattice contraposition law. $)
  chsscon2 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A C_ ( _|_ ` B ) <-> B C_ ( _|_ ` A ) ) ) $=
    ( cch wcel wa cort cfv wss wb chsscon3 choccl sylan2 wceq ococ adantl
    sseq1d bitrd ) ACDZBCDZEZABFGZHZUAFGZAFGZHZBUDHRUACDUBUEISAUAJBKLTUCBUDSUCB
    MRBNOPQ $.
    $( [26-Jun-2004] $) $( [21-Jun-2004] $)

  $( Hilbert lattice contraposition law for strict ordering. $)
  chpsscon3 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A C. B <-> ( _|_ ` B ) C. ( _|_ ` A ) ) ) $=
    ( cch wcel wa wss wn cort cfv wpss chsscon3 wb ancoms notbid anbi12d
    dfpss3 3bitr4g ) ACDZBCDZEZABFZBAFZGZEBHIZAHIZFZUEUDFZGZEABJUDUEJTUAUFUCUHA
    BKTUBUGSRUBUGLBAKMNOABPUDUEPQ $.
    $( [20-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice contraposition law for strict ordering. $)
  chpsscon1 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( ( _|_ ` A ) C. B <-> ( _|_ ` B ) C. A ) ) $=
    ( cch wcel wa cort cfv wpss wb chpsscon3 choccl sylan wceq ococ adantr
    psseq2d bitrd ) ACDZBCDZEZAFGZBHZBFGZUAFGZHZUCAHUACDSUBUEIRUABJAKLTUDAUCRUD
    AMSANOPQ $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice contraposition law for strict ordering. $)
  chpsscon2 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A C. ( _|_ ` B ) <-> B C. ( _|_ ` A ) ) ) $=
    ( cch wcel wa cort cfv wpss wb chpsscon3 choccl sylan2 wceq ococ adantl
    psseq1d bitrd ) ACDZBCDZEZABFGZHZUAFGZAFGZHZBUDHRUACDUBUEISAUAJBKLTUCBUDSUC
    BMRBNOPQ $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Commutative law for Hilbert lattice join. $)
  chjcom $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH B ) = ( B vH A ) ) $=
    ( csh wcel chj co wceq cch shjcom chsh syl2an ) ACDBCDABEFBAEFGAHDBHDABIAJB
    JK $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice join is greater than or equal to its first argument. $)
  chub1 $p |- ( ( A e. CH /\ B e. CH ) -> A C_ ( A vH B ) ) $=
    ( csh wcel chj co wss cch shub1 chsh syl2an ) ACDBCDAABEFGAHDBHDABIAJBJK $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice join is greater than or equal to its second argument. $)
  chub2 $p |- ( ( A e. CH /\ B e. CH ) -> A C_ ( B vH A ) ) $=
    ( cch wcel wa chj co chub1 chjcom sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice join is the least upper bound of two elements. $)
  chlub $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
               ( ( A C_ C /\ B C_ C ) <-> ( A vH B ) C_ C ) ) $=
    ( csh wcel cch wss wa chj co wb shlub chsh syl3an2 syl3an1 ) ADEZBFEZCFEZAC
    GBCGHABIJCGKZAFEPBDERSQABCLBMNAMO $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Add join to both sides of Hilbert lattice ordering. $)
  chlej1 $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\
                A C_ B ) -> ( A vH C ) C_ ( B vH C ) ) $=
    ( csh wcel wss chj co cch shlej1 chsh syl3anl ) ADEBDECDEABFACGHBCGHFAIEBIE
    CIEABCJAKBKCKL $.
    $( [17-Jun-2007] $) $( [22-Jun-2004] $)

  $( Add join to both sides of Hilbert lattice ordering. $)
  chlej2 $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\
                A C_ B ) -> ( C vH A ) C_ ( C vH B ) ) $=
    ( csh wcel wss chj co cch shlej2 chsh syl3anl ) ADEBDECDEABFCAGHCBGHFAIEBIE
    CIEABCJAKBKCKL $.
    $( [4-Jan-2007] $) $( [22-Jun-2004] $)

  $( Hilbert lattice ordering in terms of join. $)
  chlejb1 $p |- ( ( A e. CH /\ B e. CH ) -> ( A C_ B <-> ( A vH B ) = B ) ) $=
    ( cch wcel wss chj co wceq wb c0h cif sseq1 opreq1 eqeq1d bibi12d sseq2
    opreq2 id eqeq12d h0elch elimel chlejb1i dedth2h ) ACDZBCDZABEZABFGZBHZIUDA
    JKZBEZUIBFGZBHZIUIUEBJKZEZUIUMFGZUMHZIABJJAUIHZUFUJUHULAUIBLUQUGUKBAUIBFMNO
    BUMHZUJUNULUPBUMUIPURUKUOBUMBUMUIFQURRSOUIUMAJCTUABJCTUAUBUC $.
    $( [2-Jul-2004] $) $( [30-Jun-2004] $)

  $( Hilbert lattice ordering in terms of join. $)
  chlejb2 $p |- ( ( A e. CH /\ B e. CH ) -> ( A C_ B <-> ( B vH A ) = B ) ) $=
    ( cch wcel wa wss chj co wceq chlejb1 chjcom eqeq1d bitrd ) ACDBCDEZABFABGH
    ZBIBAGHZBIABJNOPBABKLM $.
    $( [2-Jul-2004] $) $( [2-Jul-2004] $)

  $( Equivalent expressions for "not less than" in the Hilbert lattice. $)
  chnle $p |- ( ( A e. CH /\ B e. CH ) ->
               ( -. B C_ A <-> A C. ( A vH B ) ) ) $=
    ( cch wcel wss wn chj co wpss wb c0h cif wceq sseq2 notbid id opreq1
    psseq12d bibi12d sseq1 opreq2 psseq2d h0elch elimel chnlei dedth2h ) ACDZBC
    DZBAEZFZAABGHZIZJBUGAKLZEZFZUMUMBGHZIZJUHBKLZUMEZFZUMUMURGHZIZJABKKAUMMZUJU
    OULUQVCUIUNAUMBNOVCAUMUKUPVCPAUMBGQRSBURMZUOUTUQVBVDUNUSBURUMTOVDUPVAUMBURU
    MGUAUBSUMURAKCUCUDBKCUCUDUEUF $.
    $( [27-Jun-2004] $) $( [9-Jun-2004] $)

  $( The join of a closed subspace and its orthocomplement is all of Hilbert
     space. $)
  chjo $p |- ( A e. CH -> ( A vH ( _|_ ` A ) ) = ~H ) $=
    ( cch wcel cort cfv chj co chil wceq cif id fveq2 opreq12d eqeq1d helch
    elimel chjoi dedth ) ABCZAADEZFGZHISAHJZUBDEZFGZHIAHAUBIZUAUDHUEAUBTUCFUEKA
    UBDLMNUBAHBOPQR $.
    $( [26-Nov-2005] $) $( [31-Oct-2005] $)

  $( Hilbert lattice absorption law.  From definition of lattice in [Kalmbach]
     p. 14. $)
  chabs1 $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH ( A i^i B ) ) = A ) $=
    ( cch wcel wa cin chj co wss ssid inss1 pm3.2i wb chlub pm3.26 chincl
    syl3anc mpbii chub1 syldan eqssd ) ACDZBCDZEZAABFZGHZAUDAAIZUEAIZEZUFAIZUGU
    HAJABKLUBUECDZUBUIUJMUDAUEANUBUCOZABPZULQRUBUCUKAUFIUMAUESTUA $.
    $( [27-Jun-2004] $) $( [15-Jun-2004] $)

  $( Hilbert lattice absorption law.  From definition of lattice in [Kalmbach]
     p. 14. $)
  chabs2 $p |- ( ( A e. CH /\ B e. CH ) -> ( A i^i ( A vH B ) ) = A ) $=
    ( cch wcel wa chj co cin wss wceq chub1 ssid jctil ssin sylib inss1 eqss
    sylibr ) ACDBCDEZAABFGZHZAIZAUAIZEUAAJSUCUBSAAIZATIZEUCSUEUDABKALMAATNOATPM
    UAAQR $.
    $( [27-Jun-2004] $) $( [16-Jun-2004] $)

  ${
    chabs.1 $e |- A e. CH $.
    chabs.2 $e |- B e. CH $.
    $( Hilbert lattice absorption law.  From definition of lattice in
       [Kalmbach] p. 14. $)
    chabs1i $p |- ( A vH ( A i^i B ) ) = A $=
      ( cch wcel cin chj co wceq chabs1 mp2an ) AEFBEFAABGHIAJCDABKL $.
      $( [27-Jun-2004] $) $( [10-Jun-2004] $)

    $( Hilbert lattice absorption law.  From definition of lattice in
       [Kalmbach] p. 14. $)
    chabs2i $p |- ( A i^i ( A vH B ) ) = A $=
      ( cch wcel chj co cin wceq chabs2 mp2an ) AEFBEFAABGHIAJCDABKL $.
      $( [27-Jun-2004] $) $( [16-Jun-2004] $)
  $}

  $( Idempotent law for Hilbert lattice join. $)
  chjidm $p |- ( A e. CH -> ( A vH A ) = A ) $=
    ( cch wcel cin chj co wceq chabs1 anidms inidm opreq2i syl5eqr ) ABCZAAADZE
    FZAAAEFMOAGAAHINAAEAJKL $.
    $( [27-Jun-2004] $) $( [26-Jun-2004] $)

  ${
    chjidm.1 $e |- A e. CH $.
    $( Idempotent law for Hilbert lattice join. $)
    chjidmi $p |- ( A vH A ) = A $=
      ( cch wcel chj co wceq chjidm ax-mp ) ACDAAEFAGBAHI $.
      $( [27-Jun-2004] $) $( [15-Jun-2004] $)
  $}

  ${
    chj12.1 $e |- A e. CH $.
    chj12.2 $e |- B e. CH $.
    chj12.3 $e |- C e. CH $.
    $( A rearrangement of Hilbert lattice join. $)
    chj12i $p |- ( A vH ( B vH C ) ) = ( B vH ( A vH C ) ) $=
      ( chj co chjcomi opreq1i chjassi 3eqtr3i ) ABGHZCGHBAGHZCGHABCGHGHBACGHGH
      MNCGABDEIJABCDEFKBACEDFKL $.
      $( [11-May-2006] $) $( [29-Apr-2006] $)

    ${
      chj4.4 $e |- D e. CH $.
      $( Rearrangement of the join of 4 Hilbert lattice elements. $)
      chj4i $p |- ( ( A vH B ) vH ( C vH D ) ) =
                 ( ( A vH C ) vH ( B vH D ) ) $=
        ( chj co chj12i opreq2i chjcli chjassi 3eqtr4i ) ABCDIJZIJZIJACBDIJZIJZ
        IJABIJPIJACIJRIJQSAIBCDFGHKLABPEFCDGHMNACREGBDFHMNO $.
        $( [14-May-2006] $) $( [29-Apr-2006] $)
    $}

    $( Hilbert lattice join distributes over itself. $)
    chjjdiri $p |- ( ( A vH B ) vH C ) = ( ( A vH C ) vH ( B vH C ) ) $=
      ( chj co chjidmi opreq2i chj4i eqtr3i ) ABGHZCCGHZGHMCGHACGHBCGHGHNCMGCFI
      JABCCDEFFKL $.
      $( [14-May-2006] $) $( [29-Apr-2006] $)
  $}

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm1 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A i^i B ) ) = ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) $=
    ( cch wcel cin cort cfv chj co wceq chil cif ineq1 fveq2d fveq2 opreq1d
    eqeq12d ineq2 opreq2d helch elimel chdmm1i dedth2h ) ACDZBCDZABEZFGZAFGZBFG
    ZHIZJUDAKLZBEZFGZUKFGZUIHIZJUKUEBKLZEZFGZUNUPFGZHIZJABKKAUKJZUGUMUJUOVAUFUL
    FAUKBMNVAUHUNUIHAUKFOPQBUPJZUMURUOUTVBULUQFBUPUKRNVBUIUSUNHBUPFOSQUKUPAKCTU
    ABKCTUAUBUC $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm2 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) i^i B ) ) = ( A vH ( _|_ ` B ) ) ) $=
    ( cch wcel wa cort cfv cin chj co wceq chdmm1 choccl sylan ococ adantr
    opreq1d eqtrd ) ACDZBCDZEZAFGZBHFGZUBFGZBFGZIJZAUEIJUBCDTUCUFKSUBBLAMNUAUDA
    UEISUDAKTAOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm3 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A i^i ( _|_ ` B ) ) ) = ( ( _|_ ` A ) vH B ) ) $=
    ( cch wcel wa cort cfv cin chj co wceq chdmm1 choccl sylan2 ococ adantl
    opreq2d eqtrd ) ACDZBCDZEZABFGZHFGZAFGZUBFGZIJZUDBIJSUBCDUCUFKTAUBLBMNUAUEB
    UDITUEBKSBOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm4 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) i^i ( _|_ ` B ) ) ) = ( A vH B ) ) $=
    ( cch wcel wa cort cfv cin chj co wceq chdmm2 choccl sylan2 ococ adantl
    opreq2d eqtrd ) ACDZBCDZEZAFGBFGZHFGZAUBFGZIJZABIJSUBCDUCUEKTAUBLBMNUAUDBAI
    TUDBKSBOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj1 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A vH B ) ) = ( ( _|_ ` A ) i^i ( _|_ ` B ) ) ) $=
    ( cch wcel wa cort cfv cin chj co chdmm4 fveq2d wceq chincl choccl syl2an
    ococ syl eqtr3d ) ACDZBCDZEZAFGZBFGZHZFGZFGZABIJZFGUEUBUFUHFABKLUBUECDZUGUE
    MUCCDUDCDUITUAUCUDNAOBOPUEQRS $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj2 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) vH B ) ) = ( A i^i ( _|_ ` B ) ) ) $=
    ( cch wcel wa cort cfv chj co cin wceq chdmj1 choccl sylan ococ ineq1d
    adantr eqtrd ) ACDZBCDZEAFGZBHIFGZUAFGZBFGZJZAUDJZUACDTUBUEKSUABLAMNSUEUFKT
    SUCAUDAOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj3 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A vH ( _|_ ` B ) ) ) = ( ( _|_ ` A ) i^i B ) ) $=
    ( cch wcel wa cort cfv chj co cin wceq chdmj1 choccl sylan2 ococ adantl
    ineq2d eqtrd ) ACDZBCDZEZABFGZHIFGZAFGZUBFGZJZUDBJSUBCDUCUFKTAUBLBMNUAUEBUD
    TUEBKSBOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj4 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) = ( A i^i B ) ) $=
    ( cch wcel wa cort cfv chj co cin wceq chdmj2 choccl sylan2 ococ adantl
    ineq2d eqtrd ) ACDZBCDZEZAFGBFGZHIFGZAUBFGZJZABJSUBCDUCUEKTAUBLBMNUAUDBATUD
    BKSBOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( Associative law for Hilbert lattice join.  From definition of lattice in
     [Kalmbach] p. 14. $)
  chjass $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
                ( ( A vH B ) vH C ) = ( A vH ( B vH C ) ) ) $=
    ( cch wcel chj co wceq chil cif opreq1 opreq1d eqeq12d opreq2 opreq2d
    helch elimel chjassi dedth3h ) ADEZBDEZCDEZABFGZCFGZABCFGZFGZHTAIJZBFGZCFGZ
    UGUEFGZHUGUABIJZFGZCFGZUGUKCFGZFGZHULUBCIJZFGZUGUKUPFGZFGZHABCIIIAUGHZUDUIU
    FUJUTUCUHCFAUGBFKLAUGUEFKMBUKHZUIUMUJUOVAUHULCFBUKUGFNLVAUEUNUGFBUKCFKOMCUP
    HZUMUQUOUSCUPULFNVBUNURUGFCUPUKFNOMUGUKUPAIDPQBIDPQCIDPQRS $.
    $( [28-Jun-2004] $) $( [10-Jun-2004] $)

  $( A rearrangement of Hilbert lattice join. $)
  chj12 $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
              ( A vH ( B vH C ) ) = ( B vH ( A vH C ) ) ) $=
    ( cch wcel w3a chj co wceq chjcom 3adant3 opreq1d chjass 3com12 3eqtr3d )
    ADEZBDEZCDEZFZABGHZCGHBAGHZCGHZABCGHGHBACGHGHZSTUACGPQTUAIRABJKLABCMQPRUBUC
    IBACMNO $.
    $( [27-Jun-2006] $) $( [15-Jun-2006] $)

  $( Rearrangement of the join of 4 Hilbert lattice elements. $)
  chj4 $p |- ( ( ( A e. CH /\ B e. CH ) /\ ( C e. CH /\ D e. CH ) ) ->
              ( ( A vH B ) vH ( C vH D ) ) = ( ( A vH C ) vH ( B vH D ) ) ) $=
    ( cch wcel wa chj co wceq chj12 3expb adantll opreq2d chjass 3expa chjcl
    sylan2 an4s 3eqtr4d ) AEFZBEFZGZCEFZDEFZGZGZABCDHIZHIZHIZACBDHIZHIZHIZABHIU
    HHIZACHIUKHIZUGUIULAHUBUFUIULJZUAUBUDUEUPBCDKLMNUCUHEFZUNUJJZUFUAUBUQURABUH
    OPCDQRUAUDUBUEUOUMJZUAUDGUKEFZUSUBUEGUAUDUTUSACUKOPBDQRST $.
    $( [27-Jun-2006] $) $( [15-Jun-2006] $)

  ${
    ledi.1 $e |- A e. CH $.
    ledi.2 $e |- B e. CH $.
    ledi.3 $e |- C e. CH $.
    $( An ortholattice is distributive in one ordering direction. $)
    ledii $p |- ( ( A i^i B ) vH ( A i^i C ) ) C_ ( A i^i ( B vH C ) ) $=
      ( cin chj co wss inss1 chincli chlubii mp2an inss2 chlej12i ssini ) ABGZA
      CGZHIZABCHIZRAJSAJTAJABKACKRSAABDELZACDFLZDMNRBJSCJTUAJABOACORBSCUBEUCFPN
      Q $.
      $( [11-Aug-2004] $) $( [6-Aug-2004] $)

    $( An ortholattice is distributive in one ordering direction. $)
    lediri $p |- ( ( A i^i C ) vH ( B i^i C ) ) C_ ( ( A vH B ) i^i C ) $=
      ( cin chj co ledii incom opreq12i 3sstr4i ) CAGZCBGZHICABHIZGACGZBCGZHIPC
      GCABFDEJQNROHACKBCKLPCKM $.
      $( [14-May-2006] $) $( [27-Apr-2006] $)

    $( An ortholattice is distributive in one ordering direction (join
       version). $)
    lejdii $p |- ( A vH ( B i^i C ) ) C_ ( ( A vH B ) i^i ( A vH C ) ) $=
      ( cin chj co wss wa chincli chjcli chlubi bicomi chub1i ssini inss1
      chub2i sstri inss2 mpbir2an ) ABCGZHIABHIZACHIZGZJZAUFJZUCUFJZUHUIKUGAUCU
      FDBCEFLUDUEABDEMACDFMLNOAUDUEABDEPACDFPQUCUDUEUCBUDBCRBAEDSTUCCUEBCUACAFD
      STQUB $.
      $( [16-May-2006] $) $( [27-Apr-2006] $)

    $( An ortholattice is distributive in one ordering direction (join
       version). $)
    lejdiri $p |- ( ( A i^i B ) vH C ) C_ ( ( A vH C ) i^i ( B vH C ) ) $=
      ( cin chj co lejdii chincli chjcomi ineq12i 3sstr4i ) CABGZHICAHIZCBHIZGO
      CHIACHIZBCHIZGCABFDEJOCABDEKFLRPSQACDFLBCEFLMN $.
      $( [20-May-2006] $) $( [27-Apr-2006] $)
  $}

  $( An ortholattice is distributive in one ordering direction. $)
  ledi $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
              ( ( A i^i B ) vH ( A i^i C ) ) C_ ( A i^i ( B vH C ) ) ) $=
    ( cch wcel cin chj co wss c0h cif wceq ineq1 opreq12d sseq12d ineq2
    opreq1d opreq1 ineq2d opreq2d opreq2 h0elch elimel ledii dedth3h ) ADEZBDEZ
    CDEZABFZACFZGHZABCGHZFZIUFAJKZBFZUNCFZGHZUNULFZIUNUGBJKZFZUPGHZUNUSCGHZFZIU
    TUNUHCJKZFZGHZUNUSVDGHZFZIABCJJJAUNLZUKUQUMURVIUIUOUJUPGAUNBMAUNCMNAUNULMOB
    USLZUQVAURVCVJUOUTUPGBUSUNPQVJULVBUNBUSCGRSOCVDLZVAVFVCVHVKUPVEUTGCVDUNPTVK
    VBVGUNCVDUSGUASOUNUSVDAJDUBUCBJDUBUCCJDUBUCUDUE $.
    $( [15-Jun-2006] $) $( [14-Jun-2006] $)

