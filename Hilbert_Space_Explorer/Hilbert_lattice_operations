$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Hilbert lattice operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The zero subspace is the smallest subspace. $)
  sh0let $p |- ( A e. SH -> 0H (_ A ) $=
    ( csh wcel c0v csn c0h wss sh0 snssi syl df-ch0 syl5ss ) ABCZDEZAFMDACNAGAH
    DAIJKL $.
    $( [4-Jun-2004] $) $( [3-Jun-2004] $)

  $( The zero subspace is the smallest member of ` CH ` . $)
  ch0let $p |- ( A e. CH -> 0H (_ A ) $=
    ( cch wcel csh c0h wss chsh sh0let syl ) ABCADCEAFAGAHI $.
    $( [14-Aug-2002] $)

  $( No subspace is smaller than the zero subspace. $)
  shle0t $p |- ( A e. SH -> ( A (_ 0H <-> A = 0H ) ) $=
    ( csh wcel c0h wss wa wceq sh0let biantrud eqss syl6bbr ) ABCZADEZMDAEZFADG
    LNMAHIADJK $.
    $( [26-Nov-2004] $) $( [24-Nov-2004] $)

  $( No Hilbert lattice element is smaller than zero. $)
  chle0t $p |- ( A e. CH -> ( A (_ 0H <-> A = 0H ) ) $=
    ( cch wcel csh c0h wss wceq wb chsh shle0t syl ) ABCADCAEFAEGHAIAJK $.
    $( [14-Aug-2002] $)

  $( A Hilbert lattice element that is not a subset of another is nonzero. $)
  chnlen0 $p |- ( B e. CH -> ( -. A (_ B -> -. A = 0H ) ) $=
    ( cch wcel c0h wceq wss sseq1 ch0let syl5cbir con3d ) BCDZAEFZABGZMNEBGLAEB
    HBIJK $.
    $( [2-Jul-2004] $) $( [30-Jun-2004] $)

  $( The zero subspace is a proper subset of non-zero Hilbert lattice
     elements. $)
  ch0psst $p |- ( A e. CH -> ( 0H (. A <-> A =/= 0H ) ) $=
    ( cch wcel c0h wne wss wa wpss ch0let biantrurd necom syl5bbr df-pss
    syl6rbbr ) ABCZADEZDAFZDAEZGZDAHORSPOQRAIJDAKLDAMN $.
    $( [18-Jun-2006] $) $( [9-Jun-2004] $)

  $( The intersection of orthogonal subspaces is the zero subspace. $)
  orthin $p |- ( ( A e. SH /\ B e. SH ) ->
               ( A (_ ( _|_ ` B ) -> ( A i^i B ) = 0H ) ) $=
    ( csh wcel wa cort cfv wss cin c0h wceq wi ocin sseq2d ssrin incom syl6ss
    syl5bi adantl shinclt sh0let syl jctird eqss syl6ibr ) ACDZBCDZEZABFGZHZABI
    ZJHZJUKHZEUKJKUHUJULUMUGUJULLUFUGUKBUIIZHULUJUGUNJUKBMNUJUKUIBIUNAUIBOUIBPQ
    RSUHUKCDUMABTUKUAUBUCUKJUDUE $.
    $( [24-Jun-2004] $) $( [24-Jun-2004] $)

  ${
    $d x y A $.
    shne0.1 $e |- A e. SH $.
    $( A non-zero subspace has a non-zero vector. $)
    shne0 $p |- ( A =/= 0H <-> E. x e. A x =/= 0h ) $=
      ( c0h wne wceq wn cv wcel wrex c0v df-ne wa wex wss df-rex nss csh wb
      shle0t ax-mp negbii 3bitr2r elch0 necon3bbii rexbii 3bitr ) BDEBDFZGZAHZD
      IZGZABJZUJKEZABJBDLUMUJBIULMANBDOZGUIULABPABDQUOUHBRIUOUHSCBTUAUBUCULUNAB
      UKUJKUJUDUEUFUG $.
      $( [18-Mar-2006] $) $( [25-Feb-2006] $)

    $( Hilbert subspace sum with the zero subspace. $)
    shs0 $p |- ( A +H 0H ) = A $=
      ( c0h cph co cun cspn cfv h0elsh shsumval3 wss wceq csh wcel sh0let
      ax-mp ssequn2 mpbi fveq2i spanid 3eqtr ) ACDEACFZGHAGHZAACBIJUBAGCAKZUBAL
      AMNZUDBAOPCAQRSUEUCALBATPUA $.
      $( [17-Jan-2005] $) $( [14-Jan-2005] $)

    ${
      shs00.2 $e |- B e. SH $.
      $( Two subspaces are zero iff their join is zero. $)
      shs00 $p |- ( ( A = 0H /\ B = 0H ) <-> ( A +H B ) = 0H ) $=
        ( c0h wceq wa cph co opreq12 h0elsh shs0 syl6eq wss shsub1 sseq2 mpbii
        csh wcel wb shle0t ax-mp sylib shsub2 jca impbi ) AEFZBEFZGZABHIZEFZUIU
        JEEHIEAEBEHJEKLMUKUGUHUKAENZUGUKAUJNULABCDOUJEAPQARSULUGTCAUAUBUCUKBENZ
        UHUKBUJNUMBADCUDUJEBPQBRSUMUHTDBUAUBUCUEUF $.
        $( [11-Aug-2004] $) $( [7-Aug-2004] $)
    $}
  $}

  ${
    $d x y A $.
    ch0le.1 $e |- A e. CH $.
    $( The closed subspace zero is the smallest member of ` CH ` . $)
    ch0le $p |- 0H (_ A $=
      ( cch wcel c0h wss ch0let ax-mp ) ACDEAFBAGH $.
      $( [15-Oct-1999] $)

    $( No Hilbert closed subspace is smaller than zero. $)
    chle0 $p |- ( A (_ 0H <-> A = 0H ) $=
      ( cch wcel c0h wss wceq wb chle0t ax-mp ) ACDAEFAEGHBAIJ $.
      $( [7-Apr-2001] $)

    $( A non-zero closed subspace has a non-zero vector. $)
    chne0 $p |- ( A =/= 0H <-> E. x e. A x =/= 0h ) $=
      ( chshi shne0 ) ABBCDE $.
      $( [19-Mar-2006] $) $( [25-Feb-2006] $)

    $( Intersection of a closed subspace and its orthocomplement.  Part of
       Proposition 1 of [Kalmbach] p. 65. $)
    chocin $p |- ( A i^i ( _|_ ` A ) ) = 0H $=
      ( csh wcel cort cfv cin c0h wceq chshi ocin ax-mp ) ACDAAEFGHIABJAKL $.
      $( [6-Aug-2004] $) $( [11-Oct-1999] $)

    $( Join with lattice zero in ` CH ` . $)
    chj0 $p |- ( A vH 0H ) = A $=
      ( c0h chj co cun cort cfv h0elch chjval wss wceq ch0le ssequn2 mpbi
      fveq2i pjococ 3eqtr ) ACDEACFZGHZGHAGHZGHAACBIJTUAGSAGCAKSALABMCANOPPABQR
      $.
      $( [15-Oct-1999] $)

    $( Meet with lattice one in ` CH ` . $)
    chm1 $p |- ( A i^i H~ ) = A $=
      ( chil cin inss1 ssid chssi ssini eqssi ) ACDAACEAACAFABGHI $.
      $( [24-Oct-1999] $)

    ${
      $d x y B $.  $d x y C $.
      chjcl.2 $e |- B e. CH $.
      $( Closure of ` CH ` join. $)
      chjcl $p |- ( A vH B ) e. CH $=
        ( chshi shjcl ) ABACEBDEF $.
        $( [29-Jul-1999] $)

      $( Subspace sum is smaller than subspace join.  Remark in [Kalmbach]
         p. 65. $)
      chslej $p |- ( A +H B ) (_ ( A vH B ) $=
        ( chshi shslej ) ABACEBDEF $.
        $( [17-Oct-1999] $)

      $( Membership in subspace sum. $)
      chsel $p |- ( C e. ( A +H B ) <-> E. x e. A E. y e. B C = ( x +h y ) ) $=
        ( chshi shsel ) ABCDECFHDGHI $.
        $( [19-Oct-1999] $)

      $( Closure of Hilbert lattice intersection. $)
      chincl $p |- ( A i^i B ) e. CH $=
        ( cpr cint cin cch elisseti intpr wss c0 wne wcel wa pm3.2i prss mpbi
        prnz chintcl eqeltrr ) ABEZFABGHABAHCIZBHDIZJUBUBHKZUBLMAHNZBHNZOUEUFUG
        CDPABHUCUDQRABUCSPTUA $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chsscon3 $p |- ( A (_ B <-> ( _|_ ` B ) (_ ( _|_ ` A ) ) $=
        ( wss cort cfv chil wi chssi occont mp2an choccl pjococ 3sstr3g impbi
        ) ABEZBFGZAFGZEZAHEBHEQTIACJBDJABKLTSFGZRFGZABRHESHETUAUBEIRBDMJSACMJRS
        KLACNBDNOP $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chsscon1 $p |- ( ( _|_ ` A ) (_ B <-> ( _|_ ` B ) (_ A ) $=
        ( cort cfv wss choccl chsscon3 pjococ sseq2i bitr ) AEFZBGBEFZMEFZGNAGM
        BACHDIOANACJKL $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chsscon2 $p |- ( A (_ ( _|_ ` B ) <-> B (_ ( _|_ ` A ) ) $=
        ( cort cfv wss choccl chsscon3 pjococ sseq1i bitr ) ABEFZGMEFZAEFZGBOGA
        MCBDHINBOBDJKL $.
        $( [15-Oct-1999] $)

      $( Hilbert lattice contraposition law. $)
      chcon2 $p |- ( A = ( _|_ ` B ) <-> B = ( _|_ ` A ) ) $=
        ( cort cfv wss wa wceq chsscon2 chsscon1 anbi12i eqss 3bitr4 ) ABEFZGZO
        AGZHBAEFZGZRBGZHAOIBRIPSQTABCDJBADCKLAOMBRMN $.
        $( [24-Jun-2004] $) $( [24-Jun-2004] $)

      $( Hilbert lattice contraposition law. $)
      chcon1 $p |- ( ( _|_ ` A ) = B <-> ( _|_ ` B ) = A ) $=
        ( cort cfv wceq chcon2 eqcom 3bitr4 ) BAEFZGABEFZGKBGLAGBADCHKBILAIJ $.
        $( [3-Oct-2006] $) $( [15-Jun-2006] $)

      $( Hilbert lattice contraposition law. $)
      chcon3 $p |- ( A = B <-> ( _|_ ` B ) = ( _|_ ` A ) ) $=
        ( wss wa cort cfv wceq chsscon3 anbi12i eqss 3bitr4 ) ABEZBAEZFBGHZAGHZ
        EZQPEZFABIPQINROSABCDJBADCJKABLPQLM $.
        $( [25-Jun-2004] $) $( [24-Jun-2004] $)

      $( Union is smaller than ` CH ` join. $)
      chunssj $p |- ( A u. B ) (_ ( A vH B ) $=
        ( chshi shunssj ) ABACEBDEF $.
        $( [15-Oct-1999] $)

      $( Commutative law for join in ` CH ` . $)
      chjcom $p |- ( A vH B ) = ( B vH A ) $=
        ( chshi shjcom ) ABACEBDEF $.
        $( [14-Oct-1999] $)

      $( ` CH ` join is an upper bound of two elements. $)
      chub1 $p |- A (_ ( A vH B ) $=
        ( chshi shub1 ) ABACEBDEF $.
        $( [15-Oct-1999] $)

      $( ` CH ` join is an upper bound of two elements. $)
      chub2 $p |- A (_ ( B vH A ) $=
        ( chj co chub1 chjcom sseqtr ) AABEFBAEFABCDGABCDHI $.
        $( [5-Nov-2000] $)

      ${
        $d x y C $.
        chlub.1 $e |- C e. CH $.
        $( Hilbert lattice join is the least upper bound of two elements. $)
        chlub $p |- ( ( A (_ C /\ B (_ C ) <-> ( A vH B ) (_ C ) $=
          ( chshi shlub ) ABCADGBEGFH $.
          $( [16-Jun-2004] $) $( [11-Jun-2004] $)

        $( Hilbert lattice join is the least upper bound of two elements
           (one direction of ~ chlub ). $)
        chlubi $p |- ( ( A (_ C /\ B (_ C ) -> ( A vH B ) (_ C ) $=
          ( wss wa chj co chlub biimp ) ACGBCGHABIJCGABCDEFKL $.
          $( [15-Oct-1999] $)

        $( Add join to both sides of a Hilbert lattice ordering. $)
        chlej1 $p |- ( A (_ B -> ( A vH C ) (_ ( B vH C ) ) $=
          ( chshi shlej1 ) ABCADGBEGCFGH $.
          $( [19-Oct-1999] $)

        $( Add join to both sides of a Hilbert lattice ordering. $)
        chlej2 $p |- ( A (_ B -> ( C vH A ) (_ ( C vH B ) ) $=
          ( chshi shlej2 ) ABCADGBEGCFGH $.
          $( [19-Oct-1999] $)

        ${
          chlej12.4 $e |- D e. CH $.
          $( Add join to both sides of a Hilbert lattice ordering. $)
          chlej12 $p |- ( ( A (_ B /\ C (_ D ) ->
                       ( A vH C ) (_ ( B vH D ) ) $=
            ( wss chj co chlej1 chlej2 sylan9ss ) ABICDIACJKBCJKBDJKABCEFGLCDBG
            HFMN $.
            $( [19-Oct-1999] $)
        $}
      $}

      $( Hilbert lattice ordering in terms of join. $)
      chlejb1 $p |- ( A (_ B <-> ( A vH B ) = B ) $=
        ( wss chj co wceq wa ssid chlubi mpan2 chub2 jctir eqss sylibr wi
        chub1 sstr2 eqimss syl5 ax-mp impbi ) ABEZABFGZBHZUDUEBEZBUEEZIUFUDUGUH
        UDBBEUGBJABBCDDKLBADCMNUEBOPAUEEZUFUDQABCDRUIUGUDUFAUEBSUEBTUAUBUC $.
        $( [15-Oct-1999] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm1 $p |- ( _|_ ` ( A i^i B ) ) = ( ( _|_ ` A ) vH ( _|_ ` B ) ) $=
        ( cin cort cfv chj co wss choccl chub1 chjcl chsscon1 mpbi chub2 ssini
        chincl inss1 chsscon3 inss2 chlubi mp2an eqssi ) ABEZFGZAFGZBFGZHIZUIFG
        ZUEJUFUIJUJABUGUIJUJAJUGUHACKZBDKZLAUICUGUHUKULMZNOUHUIJUJBJUHUGULUKPBU
        IDUMNOQUIUEUMABCDRZNOUGUFJZUHUFJZUIUFJUEAJUOABSUEAUNCTOUEBJUPABUAUEBUND
        TOUGUHUFUKULUEUNKUBUCUD $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm2 $p |- ( _|_ ` ( ( _|_ ` A ) i^i B ) ) = ( A vH ( _|_ ` B ) ) $=
        ( cort cfv cin chj co choccl chdmm1 pjococ opreq1i eqtr ) AEFZBGEFOEFZB
        EFZHIAQHIOBACJDKPAQHACLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm3 $p |- ( _|_ ` ( A i^i ( _|_ ` B ) ) ) = ( ( _|_ ` A ) vH B ) $=
        ( cort cfv cin chj co choccl chdmm1 pjococ opreq2i eqtr ) ABEFZGEFAEFZO
        EFZHIPBHIAOCBDJKQBPHBDLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for meet in a Hilbert lattice. $)
      chdmm4 $p |- ( _|_ ` ( ( _|_ ` A ) i^i ( _|_ ` B ) ) ) = ( A vH B ) $=
        ( cort cfv cin chj co choccl chdmm2 pjococ opreq2i eqtr ) AEFBEFZGEFAOE
        FZHIABHIAOCBDJKPBAHBDLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj1 $p |- ( _|_ ` ( A vH B ) ) = ( ( _|_ ` A ) i^i ( _|_ ` B ) ) $=
        ( cort cfv cin chj co chdmm4 fveq2i choccl chincl pjococ eqtr3 ) AEFZBE
        FZGZEFZEFABHIZEFRSTEABCDJKRPQACLBDLMNO $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj2 $p |- ( _|_ ` ( ( _|_ ` A ) vH B ) ) = ( A i^i ( _|_ ` B ) ) $=
        ( cort cfv chj co cin choccl chdmj1 pjococ ineq1i eqtr ) AEFZBGHEFOEFZB
        EFZIAQIOBACJDKPAQACLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj3 $p |- ( _|_ ` ( A vH ( _|_ ` B ) ) ) = ( ( _|_ ` A ) i^i B ) $=
        ( cort cfv chj co cin choccl chdmj1 pjococ ineq2i eqtr ) ABEFZGHEFAEFZO
        EFZIPBIAOCBDJKQBPBDLMN $.
        $( [23-Jun-2004] $) $( [21-Jun-2004] $)

      $( DeMorgan's law for join in a Hilbert lattice. $)
      chdmj4 $p |- ( _|_ ` ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) = ( A i^i B ) $=
        ( cort cfv chj co cin choccl chdmj2 pjococ ineq2i eqtr ) AEFBEFZGHEFAOE
        FZIABIAOCBDJKPBABDLMN $.
        $( [21-Jun-2004] $) $( [21-Jun-2004] $)

      $( Equivalent expressions for "not less than" in the Hilbert lattice. $)
      chnle $p |- ( -. B (_ A <->  A (. ( A vH B ) ) $=
        ( chj co wceq wn wss wa wpss chub1 biantrur chlejb1 eqcom chjcom
        eqeq2i 3bitr negbii dfpss2 3bitr4 ) AABEFZGZHZAUBIZUDJBAIZHAUBKUEUDABCD
        LMUFUCUFBAEFZAGAUGGUCBADCNUGAOUGUBABADCPQRSAUBTUA $.
        $( [16-Jun-2004] $) $( [5-Jun-2004] $)

      ${
        chjass.3 $e |- C e. CH $.
        $( Associative law for Hilbert lattice join.  From definition of
           lattice in [Kalmbach] p. 14. $)
        chjass $p |- ( ( A vH B ) vH C ) = ( A vH ( B vH C ) ) $=
          ( chj co cort cfv cin inass chdmj1 ineq1i ineq2i 3eqtr4 fveq2i chjcl
          chdmm4 3eqtr3 ) ABGHZIJZCIJZKZIJAIJZBCGHZIJZKZIJUACGHAUFGHUDUHIUEBIJZ
          KZUCKUEUIUCKZKUDUHUEUIUCLUBUJUCABDEMNUGUKUEBCEFMOPQUACABDERFSAUFDBCEF
          RST $.
          $( [17-Jun-2004] $) $( [10-Jun-2004] $)
      $}

      $( Two Hilbert lattice elements are zero iff their join is zero. $)
      chj00 $p |- ( ( A = 0H /\ B = 0H ) <-> ( A vH B ) = 0H ) $=
        ( c0h wceq wa chj co opreq12 h0elch chj0 syl6eq wss chub1 sseq2 mpbii
        chle0 sylib chub2 jca impbi ) AEFZBEFZGZABHIZEFZUEUFEEHIEAEBEHJEKLMUGUC
        UDUGAENZUCUGAUFNUHABCDOUFEAPQACRSUGBENZUDUGBUFNUIBADCTUFEBPQBDRSUAUB $.
        $( [11-Aug-2004] $) $( [7-Aug-2004] $)
    $}

    $( The join of a closed subspace and its orthocomplement. $)
    chjo $p |- ( A vH ( _|_ ` A ) ) = H~ $=
      ( cort cfv cin c0h chj co chil incom chocin eqtr fveq2i chdmm2 choc0
      3eqtr3 ) ACDZAEZCDFCDAQGHIRFCRAQEFQAJABKLMAABBNOP $.
      $( [24-Oct-1999] $)

    $( Join with Hilbert lattice unit. $)
    chj1 $p |- ( A vH H~ ) = H~ $=
      ( chil chj co helch chjcl chssi chub2 eqssi ) ACDEZCKACBFGHCAFBIJ $.
      $( [11-Aug-2004] $) $( [6-Aug-2004] $)

    $( Meet with Hilbert lattice zero. $)
    chm0 $p |- ( A i^i 0H ) = 0H $=
      ( c0h cin inss2 ch0le ssid ssini eqssi ) ACDCACECACABFCGHI $.
      $( [11-Aug-2004] $) $( [6-Aug-2004] $)

  $}

  $( Meet with Hilbert lattice zero. $)
  chm0t $p |- ( A e. CH -> ( A i^i 0H ) = 0H ) $=
    ( cch wcel c0h cin wceq cif ineq1 eqeq1d h0elch elimel chm0 dedth ) ABCZADE
    ZDFNADGZDEZDFADAPFOQDAPDHIPADBJKLM $.
    $( [14-Jun-2006] $) $( [14-Jun-2006] $)

  ${
    shjshs.1 $e |- A e. SH $.
    shjshs.2 $e |- B e. SH $.
    $( Hilbert lattice join equals the double orthocomplement of subspace
       sum. $)
    shjshs $p |- ( A vH B ) = ( _|_ ` ( _|_ ` ( A +H B ) ) ) $=
      ( chj co cph cort cfv cun csh wcel wceq shjvalt mp2an wss shunss chil
      shssi unssi shscl occon2 ax-mp eqsstr shslej wi shjcl chssi occont occl
      chsscon1 mpbi eqssi ) ABEFZABGFZHIZHIZUNABJZHIHIZUQAKLBKLUNUSMCDABNOURUOP
      USUQPABCDQURUOABRACSBDSTUOABCDUASZUBUCUDUNHIUPPZUQUNPUOUNPZVAABCDUEUORPUN
      RPVBVAUFUTUNABCDUGZUHUOUNUIOUCUNUPVCUOUTUJUKULUM $.
      $( [1-Dec-2004] $) $( [27-Nov-2004] $)

    $( A closed subspace sum equals Hilbert lattice join.  Part of Lemma
       31.1.5 of [MaedaMaeda] p. 136. $)
    shjshsel $p |- ( ( A +H B ) e. CH <-> ( A +H B ) = ( A vH B ) ) $=
      ( cph co cch wcel chj wceq cort cfv ococt shjshs syl5req shjcl eleq1
      mpbiri impbi ) ABEFZGHZTABIFZJZUATKLKLTUBTMABCDNOUCUAUBGHABCDPTUBGQRS $.
      $( [3-Jan-2005] $) $( [30-Nov-2004] $)
  $}

  ${
    $d x A $.
    $( A non-zero closed subspace has a nonzero vector. $)
    chne0t $p |- ( A e. CH -> ( A =/= 0H <-> E. x e. A x =/= 0h ) ) $=
      ( cch wcel c0h wne cv c0v wrex wb cif wceq neeq1 rexeq1 bibi12d h0elch
      elimel chne0 dedth ) BCDZBEFZAGHFZABIZJTBEKZEFZUBAUDIZJBEBUDLUAUEUCUFBUDE
      MUBABUDNOAUDBECPQRS $.
      $( [19-Jun-2006] $) $( [25-Feb-2006] $)
  $}

  $( Intersection of a closed subspace and its orthocomplement.  Part of
     Proposition 1 of [Kalmbach] p. 65. $)
  chocint $p |- ( A e. CH -> ( A i^i ( _|_ ` A ) ) = 0H ) $=
    ( cch wcel cort cfv cin c0h wceq cif id fveq2 ineq12d eqeq1d h0elch elimel
    chocin dedth ) ABCZAADEZFZGHRAGIZUADEZFZGHAGAUAHZTUCGUDAUASUBUDJAUADKLMUAAG
    BNOPQ $.
    $( [14-Jun-2006] $) $( [13-Jun-2006] $)

  $( A closed subspace less than its orthocomplement is zero. $)
  chssoct $p |- ( A e. CH -> ( A (_ ( _|_ ` A ) <-> A = 0H ) ) $=
    ( cch wcel cort cfv wss c0h wceq cin chocint sseq2d chle0t bitrd sslin
    inidm syl5ssr syl5bi wa pm3.27 chocclt ch0let syl adantr eqsstrd ex impbid
    ) ABCZAADEZFZAGHZUGAAUHIZFZUJUIUGULAGFUJUGUKGAAJKALMUIAAIUKAAUHANAOPQUGUJUI
    UGUJRAGUHUGUJSUGGUHFZUJUGUHBCUMATUHUAUBUCUDUEUF $.
    $( [27-Jun-2006] $) $( [14-Jun-2006] $)

  $( Join with Hilbert lattice zero. $)
  chj0t $p |- ( A e. CH -> ( A vH 0H ) = A ) $=
    ( cch wcel c0h chj co wceq cif opreq1 id eqeq12d h0elch elimel chj0 dedth
    ) ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [25-Jun-2004] $) $( [22-Jun-2004] $)

  $( Subspace sum is smaller than subspace join.  Remark in [Kalmbach]
     p. 65. $)
  chslejt $p |- ( ( A e. CH /\ B e. CH ) -> ( A +H B ) (_ ( A vH B ) ) $=
    ( csh wcel cph co chj wss cch shslejt chsh syl2an ) ACDBCDABEFABGFHAIDBIDAB
    JAKBKL $.
    $( [2-Sep-2004] $) $( [12-Jul-2004] $)

  $( Closure of Hilbert lattice intersection. $)
  chinclt $p |- ( ( A e. CH /\ B e. CH ) -> ( A i^i B ) e. CH ) $=
    ( cch wcel cin chil cif wceq ineq1 eleq1d ineq2 helch elimel chincl
    dedth2h ) ACDZBCDZABEZCDPAFGZBEZCDSQBFGZEZCDABFFASHRTCASBIJBUAHTUBCBUASKJSU
    AAFCLMBFCLMNO $.
    $( [20-Jun-2004] $) $( [15-Jun-2004] $)

  $( Hilbert lattice contraposition law. $)
  chsscon3t $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A (_ B <-> ( _|_ ` B ) (_ ( _|_ ` A ) ) ) $=
    ( cch wcel wss cort cfv wb chil cif wceq sseq1 fveq2 sseq2d bibi12d sseq2
    sseq1d helch elimel chsscon3 dedth2h ) ACDZBCDZABEZBFGZAFGZEZHUBAIJZBEZUEUH
    FGZEZHUHUCBIJZEZULFGZUJEZHABIIAUHKZUDUIUGUKAUHBLUPUFUJUEAUHFMNOBULKZUIUMUKU
    OBULUHPUQUEUNUJBULFMQOUHULAICRSBICRSTUA $.
    $( [20-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice contraposition law. $)
  chsscon1t $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( ( _|_  ` A ) (_ B <-> ( _|_ ` B ) (_ A ) ) $=
    ( cch wcel wa cort cfv wss wb chsscon3t chocclt sylan wceq ococt adantr
    sseq2d bitrd ) ACDZBCDZEZAFGZBHZBFGZUAFGZHZUCAHUACDSUBUEIRUABJAKLTUDAUCRUDA
    MSANOPQ $.
    $( [25-Jun-2004] $) $( [21-Jun-2004] $)

  $( Hilbert lattice contraposition law. $)
  chsscon2t $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A (_ ( _|_ ` B ) <-> B (_ ( _|_ ` A ) ) ) $=
    ( cch wcel wa cort cfv wss wb chsscon3t chocclt sylan2 wceq ococt adantl
    sseq1d bitrd ) ACDZBCDZEZABFGZHZUAFGZAFGZHZBUDHRUACDUBUEISAUAJBKLTUCBUDSUCB
    MRBNOPQ $.
    $( [26-Jun-2004] $) $( [21-Jun-2004] $)

  $( Hilbert lattice contraposition law for strict ordering. $)
  chpsscon3t $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A (. B <-> ( _|_ ` B ) (. ( _|_ ` A ) ) ) $=
    ( cch wcel wa wss wn cort cfv wpss chsscon3t wb ancoms negbid anbi12d
    dfpss3 3bitr4g ) ACDZBCDZEZABFZBAFZGZEBHIZAHIZFZUEUDFZGZEABJUDUEJTUAUFUCUHA
    BKTUBUGSRUBUGLBAKMNOABPUDUEPQ $.
    $( [20-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice contraposition law for strict ordering. $)
  chpsscon1t $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( ( _|_  ` A ) (. B <-> ( _|_ ` B ) (. A ) ) $=
    ( cch wcel wa cort cfv wpss wb chpsscon3t chocclt sylan wceq ococt adantr
    psseq2d bitrd ) ACDZBCDZEZAFGZBHZBFGZUAFGZHZUCAHUACDSUBUEIRUABJAKLTUDAUCRUD
    AMSANOPQ $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice contraposition law for strict ordering. $)
  chpsscon2t $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( A (. ( _|_ ` B ) <-> B (. ( _|_  ` A ) ) ) $=
    ( cch wcel wa cort cfv wpss wb chpsscon3t chocclt sylan2 wceq ococt adantl
    psseq1d bitrd ) ACDZBCDZEZABFGZHZUAFGZAFGZHZBUDHRUACDUBUEISAUAJBKLTUCBUDSUC
    BMRBNOPQ $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Commutative law for Hilbert lattice join. $)
  chjcomt $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH B ) = ( B vH A ) ) $=
    ( csh wcel chj co wceq cch shjcomt chsh syl2an ) ACDBCDABEFBAEFGAHDBHDABIAJ
    BJK $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice join is greater than or equal to its first argument. $)
  chub1t $p |- ( ( A e. CH /\ B e. CH ) -> A (_ ( A vH B ) ) $=
    ( csh wcel chj co wss cch shub1t chsh syl2an ) ACDBCDAABEFGAHDBHDABIAJBJK
    $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice join is greater than or equal to its second argument. $)
  chub2t $p |- ( ( A e. CH /\ B e. CH ) -> A (_ ( B vH A ) ) $=
    ( cch wcel wa chj co chub1t chjcomt sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Hilbert lattice join is the least upper bound of two elements. $)
  chlubt $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
               ( ( A (_ C /\ B (_ C ) <-> ( A vH B ) (_ C ) ) $=
    ( csh wcel cch wss wa chj co wb shlubt chsh syl3an2 syl3an1 ) ADEZBFEZCFEZA
    CGBCGHABIJCGKZAFEPBDERSQABCLBMNAMO $.
    $( [26-Jun-2004] $) $( [12-Jun-2004] $)

  $( Add join to both sides of Hilbert lattice ordering. $)
  chlej1t $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\
                A (_ B ) -> ( A vH C ) (_ ( B vH C ) ) $=
    ( csh wcel wss chj co cch shlej1t chsh syl3anl ) ADEBDECDEABFACGHBCGHFAIEBI
    ECIEABCJAKBKCKL $.
    $( [17-Jun-2007] $) $( [22-Jun-2004] $)

  $( Add join to both sides of Hilbert lattice ordering. $)
  chlej2t $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\
                A (_ B ) -> ( C vH A ) (_ ( C vH B ) ) $=
    ( csh wcel wss chj co cch shlej2t chsh syl3anl ) ADEBDECDEABFCAGHCBGHFAIEBI
    ECIEABCJAKBKCKL $.
    $( [4-Jan-2007] $) $( [22-Jun-2004] $)

  $( Hilbert lattice ordering in terms of join. $)
  chlejb1t $p |- ( ( A e. CH /\ B e. CH ) -> ( A (_ B <-> ( A vH B ) = B ) ) $=
    ( cch wcel wss chj co wceq wb c0h cif sseq1 opreq1 eqeq1d bibi12d sseq2
    opreq2 id eqeq12d h0elch elimel chlejb1 dedth2h ) ACDZBCDZABEZABFGZBHZIUDAJ
    KZBEZUIBFGZBHZIUIUEBJKZEZUIUMFGZUMHZIABJJAUIHZUFUJUHULAUIBLUQUGUKBAUIBFMNOB
    UMHZUJUNULUPBUMUIPURUKUOBUMBUMUIFQURRSOUIUMAJCTUABJCTUAUBUC $.
    $( [2-Jul-2004] $) $( [30-Jun-2004] $)

  $( Hilbert lattice ordering in terms of join. $)
  chlejb2t $p |- ( ( A e. CH /\ B e. CH ) -> ( A (_ B <-> ( B vH A ) = B ) ) $=
    ( cch wcel wa wss chj co wceq chlejb1t chjcomt eqeq1d bitrd ) ACDBCDEZABFAB
    GHZBIBAGHZBIABJNOPBABKLM $.
    $( [2-Jul-2004] $) $( [2-Jul-2004] $)

  $( Equivalent expressions for "not less than" in the Hilbert lattice. $)
  chnlet $p |- ( ( A e. CH /\ B e. CH ) ->
               ( -. B (_ A <-> A (. ( A vH B ) ) ) $=
    ( cch wcel wss wn chj co wpss wb c0h cif wceq sseq2 negbid id opreq1
    psseq12d bibi12d sseq1 opreq2 psseq2d h0elch elimel chnle dedth2h ) ACDZBCD
    ZBAEZFZAABGHZIZJBUGAKLZEZFZUMUMBGHZIZJUHBKLZUMEZFZUMUMURGHZIZJABKKAUMMZUJUO
    ULUQVCUIUNAUMBNOVCAUMUKUPVCPAUMBGQRSBURMZUOUTUQVBVDUNUSBURUMTOVDUPVAUMBURUM
    GUAUBSUMURAKCUCUDBKCUCUDUEUF $.
    $( [27-Jun-2004] $) $( [9-Jun-2004] $)

  $( The join of a closed subspace and its orthocomplement is all of Hilbert
     space. $)
  chjot $p |- ( A e. CH -> ( A vH ( _|_ ` A ) ) = H~ ) $=
    ( cch wcel cort cfv chj co chil wceq cif id fveq2 opreq12d eqeq1d helch
    elimel chjo dedth ) ABCZAADEZFGZHISAHJZUBDEZFGZHIAHAUBIZUAUDHUEAUBTUCFUEKAU
    BDLMNUBAHBOPQR $.
    $( [26-Nov-2005] $) $( [31-Oct-2005] $)

  $( Hilbert lattice absorption law.  From definition of lattice in
     [Kalmbach] p. 14. $)
  chabs1t $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH ( A i^i B ) ) = A ) $=
    ( cch wcel wa cin chj co wss ssid inss1 pm3.2i wb chlubt pm3.26 chinclt
    syl3anc mpbii chub1t syldan eqssd ) ACDZBCDZEZAABFZGHZAUDAAIZUEAIZEZUFAIZUG
    UHAJABKLUBUECDZUBUIUJMUDAUEANUBUCOZABPZULQRUBUCUKAUFIUMAUESTUA $.
    $( [27-Jun-2004] $) $( [15-Jun-2004] $)

  $( Hilbert lattice absorption law.  From definition of lattice in
     [Kalmbach] p. 14. $)
  chabs2t $p |- ( ( A e. CH /\ B e. CH ) -> ( A i^i ( A vH B ) ) = A ) $=
    ( cch wcel wa chj co cin wss wceq chub1t ssid jctil ssin sylib inss1 eqss
    sylibr ) ACDBCDEZAABFGZHZAIZAUAIZEUAAJSUCUBSAAIZATIZEUCSUEUDABKALMAATNOATPM
    UAAQR $.
    $( [27-Jun-2004] $) $( [16-Jun-2004] $)

  ${
    chabs.1 $e |- A e. CH $.
    chabs.2 $e |- B e. CH $.
    $( Hilbert lattice absorption law.  From definition of lattice in
     [Kalmbach] p. 14. $)
    chabs1 $p |- ( A vH ( A i^i B ) ) = A $=
      ( cch wcel cin chj co wceq chabs1t mp2an ) AEFBEFAABGHIAJCDABKL $.
      $( [27-Jun-2004] $) $( [10-Jun-2004] $)

    $( Hilbert lattice absorption law.  From definition of lattice in
     [Kalmbach] p. 14. $)
    chabs2 $p |- ( A i^i ( A vH B ) ) = A $=
      ( cch wcel chj co cin wceq chabs2t mp2an ) AEFBEFAABGHIAJCDABKL $.
      $( [27-Jun-2004] $) $( [16-Jun-2004] $)
  $}

  $( Idempotent law for Hilbert lattice join. $)
  chjidmt $p |- ( A e. CH -> ( A vH A ) = A ) $=
    ( cch wcel cin chj co wceq chabs1t anidms inidm opreq2i syl5eqr ) ABCZAAADZ
    EFZAAAEFMOAGAAHINAAEAJKL $.
    $( [27-Jun-2004] $) $( [26-Jun-2004] $)

  ${
    chjidm.1 $e |- A e. CH $.
    $( Idempotent law for Hilbert lattice join. $)
    chjidm $p |- ( A vH A ) = A $=
      ( cch wcel chj co wceq chjidmt ax-mp ) ACDAAEFAGBAHI $.
      $( [27-Jun-2004] $) $( [15-Jun-2004] $)
  $}

  ${
    chj12.1 $e |- A e. CH $.
    chj12.2 $e |- B e. CH $.
    chj12.3 $e |- C e. CH $.
    $( A rearrangement of Hilbert lattice join. $)
    chj12 $p |- ( A vH ( B vH C ) ) = ( B vH ( A vH C ) ) $=
      ( chj co chjcom opreq1i chjass 3eqtr3 ) ABGHZCGHBAGHZCGHABCGHGHBACGHGHMNC
      GABDEIJABCDEFKBACEDFKL $.
      $( [11-May-2006] $) $( [29-Apr-2006] $)

    ${
      chj4.4 $e |- D e. CH $.
      $( Rearrangement of the join of 4 Hilbert lattice elements. $)
      chj4 $p |- ( ( A vH B ) vH ( C vH D ) ) =
                 ( ( A vH C ) vH ( B vH D ) ) $=
        ( chj co chj12 opreq2i chjcl chjass 3eqtr4 ) ABCDIJZIJZIJACBDIJZIJZIJAB
        IJPIJACIJRIJQSAIBCDFGHKLABPEFCDGHMNACREGBDFHMNO $.
        $( [14-May-2006] $) $( [29-Apr-2006] $)
    $}

    $( Hilbert lattice join distributes over itself. $)
    chjjdir $p |- ( ( A vH B ) vH C ) = ( ( A vH C ) vH ( B vH C ) ) $=
      ( chj co chjidm opreq2i chj4 eqtr3 ) ABGHZCCGHZGHMCGHACGHBCGHGHNCMGCFIJAB
      CCDEFFKL $.
      $( [14-May-2006] $) $( [29-Apr-2006] $)
  $}

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm1t $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A i^i B ) ) = ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) $=
    ( cch wcel cin cort cfv chj co wceq chil cif ineq1 fveq2d fveq2 opreq1d
    eqeq12d ineq2 opreq2d helch elimel chdmm1 dedth2h ) ACDZBCDZABEZFGZAFGZBFGZ
    HIZJUDAKLZBEZFGZUKFGZUIHIZJUKUEBKLZEZFGZUNUPFGZHIZJABKKAUKJZUGUMUJUOVAUFULF
    AUKBMNVAUHUNUIHAUKFOPQBUPJZUMURUOUTVBULUQFBUPUKRNVBUIUSUNHBUPFOSQUKUPAKCTUA
    BKCTUAUBUC $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm2t $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) i^i B ) ) = ( A vH ( _|_ ` B ) ) ) $=
    ( cch wcel wa cort cfv cin chj co wceq chdmm1t chocclt sylan ococt adantr
    opreq1d eqtrd ) ACDZBCDZEZAFGZBHFGZUBFGZBFGZIJZAUEIJUBCDTUCUFKSUBBLAMNUAUDA
    UEISUDAKTAOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm3t $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A i^i ( _|_ ` B ) ) ) = ( ( _|_ ` A ) vH B ) ) $=
    ( cch wcel wa cort cfv cin chj co wceq chdmm1t chocclt sylan2 ococt adantl
    opreq2d eqtrd ) ACDZBCDZEZABFGZHFGZAFGZUBFGZIJZUDBIJSUBCDUCUFKTAUBLBMNUAUEB
    UDITUEBKSBOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for meet in a Hilbert lattice. $)
  chdmm4t $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) i^i ( _|_ ` B ) ) ) = ( A vH B ) ) $=
    ( cch wcel wa cort cfv cin chj co wceq chdmm2t chocclt sylan2 ococt adantl
    opreq2d eqtrd ) ACDZBCDZEZAFGBFGZHFGZAUBFGZIJZABIJSUBCDUCUEKTAUBLBMNUAUDBAI
    TUDBKSBOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj1t $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A vH B ) ) = ( ( _|_ ` A ) i^i ( _|_ ` B ) ) ) $=
    ( cch wcel wa cort cfv cin chj co chdmm4t fveq2d wceq chinclt chocclt
    syl2an ococt syl eqtr3d ) ACDZBCDZEZAFGZBFGZHZFGZFGZABIJZFGUEUBUFUHFABKLUBU
    ECDZUGUEMUCCDUDCDUITUAUCUDNAOBOPUEQRS $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj2t $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) vH B ) ) = ( A i^i ( _|_ ` B ) ) ) $=
    ( cch wcel wa cort cfv chj co cin wceq chdmj1t chocclt sylan ococt ineq1d
    adantr eqtrd ) ACDZBCDZEAFGZBHIFGZUAFGZBFGZJZAUDJZUACDTUBUEKSUABLAMNSUEUFKT
    SUCAUDAOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj3t $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( A vH ( _|_ ` B ) ) ) = ( ( _|_ ` A ) i^i B ) ) $=
    ( cch wcel wa cort cfv chj co cin wceq chdmj1t chocclt sylan2 ococt adantl
    ineq2d eqtrd ) ACDZBCDZEZABFGZHIFGZAFGZUBFGZJZUDBJSUBCDUCUFKTAUBLBMNUAUEBUD
    TUEBKSBOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( DeMorgan's law for join in a Hilbert lattice. $)
  chdmj4t $p |- ( ( A e. CH /\ B e. CH ) ->
                ( _|_ ` ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) = ( A i^i B ) ) $=
    ( cch wcel wa cort cfv chj co cin wceq chdmj2t chocclt sylan2 ococt adantl
    ineq2d eqtrd ) ACDZBCDZEZAFGBFGZHIFGZAUBFGZJZABJSUBCDUCUEKTAUBLBMNUAUDBATUD
    BKSBOPQR $.
    $( [3-Jul-2004] $) $( [21-Jun-2004] $)

  $( Associative law for Hilbert lattice join.  From definition of lattice in
     [Kalmbach] p. 14. $)
  chjasst $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
                ( ( A vH B ) vH C ) = ( A vH ( B vH C ) ) ) $=
    ( cch wcel chj co wceq chil cif opreq1 opreq1d eqeq12d opreq2 opreq2d
    helch elimel chjass dedth3h ) ADEZBDEZCDEZABFGZCFGZABCFGZFGZHTAIJZBFGZCFGZU
    GUEFGZHABCIIIUGUABIJZFGZCFGZUGUKCFGZFGZHULUBCIJZFGZUGUKUPFGZFGZHAUGHZUDUIUF
    UJUTUCUHCFAUGBFKLAUGUEFKMBUKHZUIUMUJUOVAUHULCFBUKUGFNLVAUEUNUGFBUKCFKOMCUPH
    ZUMUQUOUSCUPULFNVBUNURUGFCUPUKFNOMUGUKUPAIDPQBIDPQCIDPQRS $.
    $( [28-Jun-2004] $) $( [10-Jun-2004] $)

  $( A rearrangement of Hilbert lattice join. $)
  chj12t $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
              ( A vH ( B vH C ) ) = ( B vH ( A vH C ) ) ) $=
    ( cch wcel w3a chj co wceq chjcomt 3adant3 opreq1d chjasst 3com12 3eqtr3d
    ) ADEZBDEZCDEZFZABGHZCGHBAGHZCGHZABCGHGHBACGHGHZSTUACGPQTUAIRABJKLABCMQPRUB
    UCIBACMNO $.
    $( [27-Jun-2006] $) $( [15-Jun-2006] $)

  $( Rearrangement of the join of 4 Hilbert lattice elements. $)
  chj4t $p |- ( ( ( A e. CH /\ B e. CH ) /\ ( C e. CH /\ D e. CH ) ) ->
              ( ( A vH B ) vH ( C vH D ) ) = ( ( A vH C ) vH ( B vH D ) ) ) $=
    ( cch wcel wa chj co wceq chj12t 3expb adantll opreq2d chjasst 3expa
    chjclt sylan2 an4s 3eqtr4d ) AEFZBEFZGZCEFZDEFZGZGZABCDHIZHIZHIZACBDHIZHIZH
    IZABHIUHHIZACHIUKHIZUGUIULAHUBUFUIULJZUAUBUDUEUPBCDKLMNUCUHEFZUNUJJZUFUAUBU
    QURABUHOPCDQRUAUDUBUEUOUMJZUAUDGUKEFZUSUBUEGUAUDUTUSACUKOPBDQRST $.
    $( [27-Jun-2006] $) $( [15-Jun-2006] $)

  ${
    ledi.1 $e |- A e. CH $.
    ledi.2 $e |- B e. CH $.
    ledi.3 $e |- C e. CH $.
    $( An ortholattice is distributive in one ordering direction. $)
    ledi $p |- ( ( A i^i B ) vH ( A i^i C ) ) (_ ( A i^i ( B vH C ) ) $=
      ( cin chj co wss inss1 chincl chlubi mp2an inss2 chlej12 ssini ) ABGZACGZ
      HIZABCHIZRAJSAJTAJABKACKRSAABDELZACDFLZDMNRBJSCJTUAJABOACORBSCUBEUCFPNQ
      $.
      $( [11-Aug-2004] $) $( [6-Aug-2004] $)

    $( An ortholattice is distributive in one ordering direction. $)
    ledir $p |- ( ( A i^i C ) vH ( B i^i C ) ) (_ ( ( A vH B ) i^i C ) $=
      ( cin chj co ledi incom opreq12i 3sstr4 ) CAGZCBGZHICABHIZGACGZBCGZHIPCGC
      ABFDEJQNROHACKBCKLPCKM $.
      $( [14-May-2006] $) $( [27-Apr-2006] $)

    $( An ortholattice is distributive in one ordering direction (join
       version). $)
    lejdi $p |- ( A vH ( B i^i C ) ) (_ ( ( A vH B ) i^i ( A vH C ) ) $=
      ( cin chj co wss wa chincl chjcl chlub bicomi chub1 ssini inss1 chub2
      sstri inss2 mpbir2an ) ABCGZHIABHIZACHIZGZJZAUFJZUCUFJZUHUIKUGAUCUFDBCEFL
      UDUEABDEMACDFMLNOAUDUEABDEPACDFPQUCUDUEUCBUDBCRBAEDSTUCCUEBCUACAFDSTQUB
      $.
      $( [16-May-2006] $) $( [27-Apr-2006] $)

    $( An ortholattice is distributive in one ordering direction (join
       version). $)
    lejdir $p |- ( ( A i^i B ) vH C ) (_ ( ( A vH C ) i^i ( B vH C ) ) $=
      ( cin chj co lejdi chincl chjcom ineq12i 3sstr4 ) CABGZHICAHIZCBHIZGOCHIA
      CHIZBCHIZGCABFDEJOCABDEKFLRPSQACDFLBCEFLMN $.
      $( [20-May-2006] $) $( [27-Apr-2006] $)
  $}

  $( An ortholattice is distributive in one ordering direction. $)
  ledit $p |- ( ( A e. CH /\ B e. CH /\ C e. CH ) ->
              ( ( A i^i B ) vH ( A i^i C ) ) (_ ( A i^i ( B vH C ) ) ) $=
    ( cch wcel cin chj co wss c0h cif wceq ineq1 opreq12d sseq12d ineq2
    opreq1d opreq1 ineq2d opreq2d opreq2 h0elch elimel ledi dedth3h ) ADEZBDEZC
    DEZABFZACFZGHZABCGHZFZIUFAJKZBFZUNCFZGHZUNULFZIABCJJJUNUGBJKZFZUPGHZUNUSCGH
    ZFZIUTUNUHCJKZFZGHZUNUSVDGHZFZIAUNLZUKUQUMURVIUIUOUJUPGAUNBMAUNCMNAUNULMOBU
    SLZUQVAURVCVJUOUTUPGBUSUNPQVJULVBUNBUSCGRSOCVDLZVAVFVCVHVKUPVEUTGCVDUNPTVKV
    BVGUNCVDUSGUASOUNUSVDAJDUBUCBJDUBUCCJDUBUCUDUE $.
    $( [15-Jun-2006] $) $( [14-Jun-2006] $)

