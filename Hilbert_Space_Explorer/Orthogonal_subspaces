$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Orthogonal subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    osumlem1.1 $e |- A e. CH $.
    osumlem1.2 $e |- B e. CH $.
    osumlem1.3 $e |- B C_ ( _|_ ` A ) $.
    osumlem1.4 $e |- ( ph <-> ( ( ( C e. A /\ D e. B ) /\ R = ( C +h D ) )
                 /\ ( ( x e. A /\ y e. ( _|_ ` A ) ) /\ z = ( x +h y ) ) ) ) $.
    $( Lemma for ~ osumi . $)
    osumlem1 $p |- ( ph -> ( ( ( C e. ~H /\ D e. ~H ) /\
          R e. ~H ) /\ ( ( x e. ~H /\ y e. ~H ) /\ z e. ~H ) ) ) $=
      ( wcel wa cva co wceq cv cort cfv chil eleq1 hvaddcl syl5cbir imdistani
      cheli anim12i sylan choccli sylbi ) AGENZHFNZOZIGHPQZRZOZBSZENZCSZETUAZNZ
      OZDSZURUTPQZRZOZOGUBNZHUBNZOZIUBNZOZURUBNZUTUBNZOZVDUBNZOZOMUQVLVGVQVJUPV
      LUNVJUPVKUPVKUOUBNVJIUOUBUCGHUDUEUFULVHUMVIGEJUGHFKUGUHUIVOVFVQVCVOVFVPVF
      VPVEUBNVOVDVEUBUCURUTUDUEUFUSVMVBVNUREJUGUTVAEJUJUGUHUIUHUK $.
      $( [19-Oct-1999] $)

    $( Lemma for ~ osumi . $)
    osumlem2 $p |- ( ph -> ( ( ( normh ` ( C -h x ) ) ^ 2 ) +
                   ( ( normh ` ( D -h y ) ) ^ 2 ) ) =
                   ( ( normh ` ( R -h z ) ) ^ 2 ) ) $=
      ( wcel wa cva co wceq cv cort cfv cmv cno c2 cexp caddc pm3.27
      opreqan12d chil biid osumlem1 hvsub4 ad2ant2r syl eqtrd fveq2d opreq1d
      csp cc0 normpyth hvsubcl anim12i an4s csh wi chshii shocorth ax-mp
      shsubclOLD choccli sseli sylan syl2an sylc eqtr2d sylbi ) AGENZHFNZOZIGHP
      QZRZOZBSZENZCSZETUAZNZOZDSZWCWEPQZRZOZOZGWCUBQZUCUAUDUEQHWEUBQZUCUAUDUEQU
      FQZIWIUBQZUCUAZUDUEQZRMWMWSWNWOPQZUCUAZUDUEQZWPWMWRXAUDUEWMWQWTUCWMWQVTWJ
      UBQZWTWBWLIVTWIWJUBVSWAUGWHWKUGUHWMGUINZHUINZOZIUINZOWCUINZWEUINZOZWIUINZ
      OOZXCWTRZWMBCDEFGHIJKLWMUJUKZXFXJXMXGXKGHWCWEULUMUNUOUPUQWNUINZWOUINZOZWN
      WOURQUSRZXBWPRWMWNWOUTWMXLXQXNXFXJXQXGXKXDXHXEXIXQXDXHOXOXEXIOXPGWCVAHWEV
      AVBVCUMUNVSWHXRWAWKVQWDVRWGXRWNENZWOWFNZXRVQWDOZVRWGOEVDNZXSXTOXRVEEJVFZW
      NWOEVGVHYBYAXSVEYCGWCEVIVHHWFNZWGXTVRWFVDNYDWGOXTVEWFEJVJVFHWEWFVIVHFWFHL
      VKVLVMVCUMVNVOVP $.
      $( [19-Oct-1999] $)

    $( Lemma for ~ osumi . $)
    osumlem3 $p |- ( ph ->
                   ( normh ` ( D -h y ) ) <_ ( normh ` ( R -h z ) ) ) $=
      ( wcel wa cva co wceq cv cort cfv cmv cno cle wbr c2 cexp cc0 caddc chil
      cr biid osumlem1 hvsubcl ad2ant2r normcl syl sqge0 3syl w3a wb 0re a1i
      resqcl ad2ant2l 3jca leadd1 mpbid cc recnd addid2 osumlem2 3brtr3d jca
      le2sq normge0 syl2an mpbird sylbi ) AGENHFNOIGHPQROBSZENCSZETUANODSZVTWAP
      QROOZHWAUBQZUCUAZIWBUBQZUCUAZUDUEZMWCWHWEUFUGQZWGUFUGQZUDUEZWCUHWIUIQZGVT
      UBQZUCUAZUFUGQZWIUIQZWIWJUDWCUHWOUDUEZWLWPUDUEZWCGUJNZHUJNZOZIUJNZOVTUJNZ
      WAUJNZOZWBUJNZOOZWNUKNZWQWCBCDEFGHIJKLWCULZUMZXGWMUJNZXHXAXEXKXBXFWSXCXKW
      TXDGVTUNUOUOZWMUPZUQWNURUSWCXGUHUKNZWOUKNZWIUKNZUTWQWRVAXJXGXNXOXPXNXGVBV
      CXGXKXHXOXLXMWNVDUSXGWDUJNZWEUKNZXPXAXEXQXBXFWTXDXQWSXCHWAUNVEUOZWDUPZWEV
      DUSZVFUHWOWIVGUSVHWCXGWIVINWLWIRXJXGWIYAVJWIVKUSWCBCDEFGHIJKLXIVLVMWCXGXQ
      WFUJNZOWHWKVAZXJXGXQYBXSXBXFYBXAXEIWBUNVEVNXRUHWEUDUEZOWGUKNZUHWGUDUEZOYC
      XQYBWEWGVOXQXRYDXTWDVPVNYBYEYFWFUPWFVPVNVQUSVRVS $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d u v w A $.  $d u v w B $.  $d u v w F $.  $d u v w G $.  $d u v w H $.
    $d u v w x $.  $d u v w y $.  $d u v w z $.
    osumlem4.1 $e |- A e. CH $.
    osumlem4.2 $e |- B e. CH $.
    osumlem4.3 $e |- B C_ ( _|_ ` A ) $.
    osumlem4.4 $e |- G e. _V $.
    osumlem4.5 $e |- H e. _V $.
    $( Lemma for ~ osumi . $)
    osumlem4 $p |- ( ( ( ( F : NN --> A /\ G : NN --> B ) /\
                   A. w e. NN ( H ` w ) = ( ( F ` w ) +h ( G ` w ) ) )
                  /\ ( ( x e. A /\ y e. ( _|_ ` A ) ) /\ z = ( x +h y ) ) ) ->
                  ( H ~~>v z -> G ~~>v y ) ) $=
      ( vu vv cn wf wa cv cfv cva co wceq wral wcel cort chli wbr chil cc0 clt
      cle cmv cno wi wrex cr ffvelrn expcom anim12d biid osumlem3 adantr
      osumlem1 lelttr 3exp hvsubcl normcl syl ad2ant2l ad2ant2r sylc imp mpand
      exp41 com24 syl6 com4l imp41 imim2 ralimdvaa exp31 ralim reximdv imim2d
      visset hlimconvi syl5 wss chssii fss mpan2 ad2antlr csh chshii shocss
      ax-mp sseli anim12i jctild hlimi syl6ibr ) QEGRZQFHRZSZDTZIUAZXGGUAZXGHUA
      ZUBUCUDZDQUEZSZATZEUFZBTZEUGUAZUFZSCTZXNXPUBUCUDZSZSZIXSUHUIZQUJHRZXPUJUF
      ZSZUKOTZULUIZPTXGUMUIZXJXPUNUCZUOUAZYGULUIZUPZDQUEZPQUQZUPZOURUEZSHXPUHUI
      YBYCYQYFYBYHYIXHXSUNUCZUOUAZYGULUIZUPZDQUEZPQUQZUPZOURUEYQYCYBUUDYPOURYBY
      GURUFZSZUUCYOYHUUFUUBYNPQUUFUUAYMUPZDQUEZUUBYNUPXFXLYAUUEUUHXFUUEYAXLUUHX
      FUUEYAXLUUHUPXFUUESYASZXKUUGDQUUIXGQUFZSXKYTYLUPZUUGXFUUEYAUUJXKUUKUPZUUJ
      XFUUEYAUULUUJXFXIEUFZXJFUFZSZUUEYAUULUPUPUUJXDUUMXEUUNXDUUJUUMQEXGGUSUTXE
      UUJUUNQFXGHUSUTVAUUOXKYAUUEUUKUUOXKYAUUEUUKUUOXKSYASZUUESYKYSUMUIZYTYLUUP
      UUQUUEUUPABCEFXIXJXHJKLUUPVBZVCVDUUPUUEUUQYTSYLUPZUUPXIUJUFZXJUJUFZSZXHUJ
      UFZSXNUJUFZYESZXSUJUFZSSZUUEUUSUPZUUPABCEFXIXJXHJKLUURVEYKURUFZYSURUFZUVH
      UVGUVIUVJUUEUUSYKYSYGVFVGUVBUVEUVIUVCUVFUVAYEUVIUUTUVDUVAYESYJUJUFUVIXJXP
      VHYJVIVJVKVLUVCUVFUVJUVBUVEUVCUVFSYRUJUFUVJXHXSVHYRVIVJVKVMVJVNVOVPVQVRVS
      VTYTYLYIWAVRWBWCVQVTUUAYMDQWDVJWEWFWBOPDXSINCWGWHWIXMYDYAYEXEYDXDXLXEFUJW
      JYDFKWKQFUJHWLWMWNXRYEXOXTXQUJXPEWOUFXQUJWJEJWPEWQWRWSWNWTXAOPDXPHMBWGXBX
      C $.
      $( [18-Oct-1999] $)
  $}

  ${
    $d x y w f g A $.  $d x y w f g B $.  $d x y w f g H $.
    osumlem5.1 $e |- A e. CH $.
    osumlem5.2 $e |- B e. CH $.
    $( Lemma for ~ osumi . $)
    osumlem5 $p |- ( H : NN --> ( A +H B ) -> E. f E. g ( ( f : NN --> A /\
     g : NN --> B ) /\ A. w e. NN ( H ` w ) = ( ( f ` w ) +h ( g ` w ) ) ) ) $=
      ( vx vy cn cph co wf cv cfv cva wceq wrex wral wa wex wcel ffvelrn ex
      chshii shseli syl6ib r19.21aiv nnex cch elisseti opreq1 eqeq2d rexbidv
      ac6 opreq2 anim2i anass exbii 19.42v bitri sylibr eximi 3syl ) KBCLMZFNZA
      OZFPZIOZJOZQMZRZJCSZIBSZAKTKBDOZNZVIVHVPPZVKQMZRZJCSZAKTZUAZDUBVQKCEOZNZU
      AVIVRVHWDPZQMZRZAKTZUAZEUBZDUBVGVOAKVGVHKUCZVIVFUCZVOVGWLWMKVFVHFUDUEIJBC
      VIBGUFCHUFUGUHUIVNWAAIKBDUJBUKGULVJVRRZVMVTJCWNVLVSVIVJVRVKQUMUNUOUPWCWKD
      WCVQWEWIUAZEUBZUAZWKWBWPVQVTWHAJKCEUJCUKHULVKWFRVSWGVIVKWFVRQUQUNUPURWKVQ
      WOUAZEUBWQWJWREVQWEWIUSUTVQWOEVAVBVCVDVE $.
      $( [18-Oct-1999] $)
  $}

  ${
    $d w f g A $.  $d w f g B $.  $d w f g H $.  $d w f g x $.  $d w f g y $.
    $d w f g z $.
    osumlem6.1 $e |- A e. CH $.
    osumlem6.2 $e |- B e. CH $.
    osumlem6.3 $e |- B C_ ( _|_ ` A ) $.
    osumlem6.4 $e |- H e. _V $.
    $( Lemma for ~ osumi . $)
    osumlem6 $p |- ( ( ( H : NN --> ( A +H B ) /\ H ~~>v z )
                  /\ ( ( x e. A /\ y e. ( _|_ ` A ) ) /\ z = ( x +h y ) ) ) ->
                  y e. B ) $=
      ( vf vg vw cn cph co wf cv chli wbr wcel cort cfv wa cva wceq wral wex
      wi osumlem5 cch visset chlimi mp3an1 simplr ad2antrr osumlem4 imp sylanc
      exp31 com23 19.23aivv syl imp31 ) NDEOPFQZFCRZSTZARZDUABRZDUBUCUAUDVFVHVI
      UEPUFUDZVIEUAZVENDKRZQZNELRZQZUDMRZFUCVPVLUCVPVNUCUEPUFMNUGZUDZLUHKUHVGVJ
      VKUIUIZMDEKLFGHUJVRVSKLVRVJVGVKVRVJVGVKVOVNVISTZVKVRVJUDZVGUDEUKUAVOVTVKH
      VIVNEBULUMUNVRVOVJVGVMVOVQUOUPWAVGVTABCMDEVLVNFGHILULJUQURUSUTVAVBVCVD $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d x y z h A $.  $d x y z h B $.
    osumlem7.1 $e |- A e. CH $.
    osumlem7.2 $e |- B e. CH $.
    ${
      osumlem7.3 $e |- B C_ ( _|_ ` A ) $.
      $( Lemma for ~ osumi . $)
      osumlem7 $p |- ( A +H B ) e. CH $=
        ( vh vz vx vy cph co cch wcel csh cn cv wf chli wbr wa wi wal
        closedsub chshii shscli cva wceq cort cfv wrex chil visset hlimveci
        pjpjth mpan syl adantl wex shsvai pm3.26 ad2antrl osumlem6 sylanc wb
        eleq1 ad2antll mpbird ex 19.23advv r2ex syl5ib mpd gen2 mpbir2an ) ABJK
        ZLMVONMOVOFPZQZVPGPZRSZTZVRVOMZUAZGUBFUBGFVOUCABACUDZBDUDZUEWBFGVTVRHPZ
        IPZUFKZUGZIAUHUIZUJHAUJZWAVSWJVQVSVRUKMZWJVRVPFULZGULUMALMWKWJCHIVRAUNU
        OUPUQVTWEAMZWFWIMZTZWHTZIURHURWAWJVTWPWAHIVTWPWAVTWPTZWAWGVOMZWMWFBMWRW
        QABWEWFWCWDUSWOWMVTWHWMWNUTVAHIGABVPCDEWLVBVCWHWAWRVDVTWOVRWGVOVEVFVGVH
        VIWHHIAWIVJVKVLVMVN $.
        $( [19-Oct-1999] $)
    $}

    $( Lemma for ~ osumi . $)
    osumlem8 $p |- ( B C_ ( _|_ ` A ) -> ( A +H B ) e. CH ) $=
      ( cort cfv wss cph co cch wcel c0h cif wceq opreq1 eleq1d opreq2 h0elch
      keepel fveq2 sseq2d sseq1 chil chssii choc0 sseqtr4i elimhyp2v osumlem7
      dedth2v ) BAEFZGZABHIZJKUKALMZBHIZJKUMUKBLMZHIZJKABLLAUMNZULUNJAUMBHOPBUO
      NUNUPJBUOUMHQPUMUOUKALJCRSUKBLJDRSUKBUMEFZGUOURGLLEFZGLURGABLLUQUJURBAUME
      TUABUOURUBLUMNUSURLLUMETUALUOURUBLUCUSLRUDUEUFUGUHUI $.
      $( [19-Oct-1999] $)

    $( If two closed subspaces of a Hilbert space are orthogonal, their
       subspace sum equals their subspace join.  Lemma 3 of [Kalmbach] p. 67.
       Note that the Axiom of Choice is used for this proof (in ~ osumlem5 and
       via ~ pjpjth in ~ osumlem7 ). $)
    osumi $p |- ( A C_ ( _|_ ` B ) -> ( A +H B ) = ( A vH B ) ) $=
      ( cort cfv wss cph co chj wa wceq cch wcel osumlem8 chshii shscomi
      syl5eqel cun shunssi chil chssii unssi shscli shssii occon2i ax-mp
      chjvali eqcomi a1i ococ sseq12d mpbii syl chsleji jctil eqss sylibr ) ABE
      FGZABHIZABJIZGZVAUTGZKUTVALUSVCVBUSUTMNZVCUSBAHIMUTBADCOABACPZBDPZQRVDABS
      ZEFEFZUTEFEFZGZVCVGUTGVJABVEVFTVGUTABUAACUBBDUBUCUTABVEVFUDUEUFUGVDVHVAVI
      UTVHVALVDVAVHABCDUHUIUJUTUKULUMUNABCDUOUPUTVAUQUR $.
      $( [24-Nov-2004] $) $( [28-Oct-1999] $)

    $( Corollary of ~ osumi . $)
    osumcori $p |- ( ( A i^i B ) +H ( A i^i ( _|_ ` B ) ) ) =
                  ( ( A i^i B ) vH ( A i^i ( _|_ ` B ) ) ) $=
      ( cin cort cfv wss cph co chj wceq inss2 choccli chub2i sstri chdmm3i
      sseqtr4i chincli osumi ax-mp ) ABEZABFGZEZFGZHUBUDIJUBUDKJLUBAFGZBKJZUEUB
      BUGABMBUFDACNOPABCDQRUBUDABCDSAUCCBDNSTUA $.
      $( [13-Jan-2005] $) $( [5-Nov-2000] $)
  $}

  $( If two closed subspaces of a Hilbert space are orthogonal, their subspace
     sum equals their subspace join.  Lemma 3 of [Kalmbach] p. 67. $)
  osum $p |- ( ( A e. CH /\ B e. CH /\ A C_ ( _|_ ` B ) ) ->
             ( A +H B ) = ( A vH B ) ) $=
    ( cch wcel cort cfv wss cph co chj wceq wi chil cif sseq1 opreq1 eqeq12d
    imbi12d fveq2 sseq2d opreq2 helch elimel osumi dedth2h 3impia ) ACDZBCDZABE
    FZGZABHIZABJIZKZUGUHUJUMLUGAMNZUIGZUNBHIZUNBJIZKZLUNUHBMNZEFZGZUNUSHIZUNUSJ
    IZKZLABMMAUNKZUJUOUMURAUNUIOVEUKUPULUQAUNBHPAUNBJPQRBUSKZUOVAURVDVFUIUTUNBU
    SESTVFUPVBUQVCBUSUNHUABUSUNJUAQRUNUSAMCUBUCBMCUBUCUDUEUF $.
    $( [12-Jan-2006] $) $( [31-Oct-2005] $)

  $( The subspace sum of a closed subspace and its complement is all of Hilbert
     space. $)
  chso $p |- ( A e. CH -> ( A +H ( _|_ ` A ) ) = ~H ) $=
    ( cch wcel cort cfv cph co chj chil wss wceq choccl csh chsh shococss syl
    osum mpd3an23 chjo eqtrd ) ABCZAADEZFGZAUBHGZIUAUBBCAUBDEJZUCUDKALUAAMCUEAN
    AOPAUBQRAST $.
    $( [17-Jan-2006] $) $( [31-Oct-2005] $)

  ${
    osumcor2.1 $e |- A e. CH $.
    osumcor2.2 $e |- B e. CH $.
    $( Corollary of ~ osumi , showing it holds under the weaker hypothesis that
       ` A ` and ` B ` commute. $)
    osumcor2i $p |- ( A C_H B ->
                   ( A +H B ) = ( A vH B ) ) $=
      ( ccm wbr cph co chj wss wa wceq cort cfv cin cmcm2i choccli cmbr4i
      bitri chjcli chincli osumi chjcomi ineq2i opreq1i eqtri pjoml4i 3eqtri
      eqeq2i inss1 chshii shlessi ax-mp sseq1 mpbii sylbi syl chsleji jctil
      eqss sylibr ) ABEFZABGHZABIHZJZVDVCJZKVCVDLVBVFVEVBAAMNZBMNZIHZOZVHJZVFVB
      AVHEFVKABCDPAVHCBDQZRSVKVJBGHZVJBIHZLZVFVJBAVICVGVHACQZVLTUAZDUBVOVMVDLZV
      FVNVDVMVNBAVHVGIHZOZIHZBAIHVDVNVTBIHWAVJVTBIVIVSAVGVHVPVLUCUDUEVTBAVSCVHV
      GVLVPTUADUCUFBADCUGBADCUCUHUIVRVMVCJZVFVJAJWBAVIUJVJABVJVQUKACUKBDUKULUMV
      MVDVCUNUOUPUQUPABCDURUSVCVDUTVA $.
      $( [13-Jan-2005] $) $( [6-Dec-2000] $)
  $}

  ${
    spansnj.1 $e |- A e. CH $.
    spansnj.2 $e |- B e. ~H $.
    $( The subspace sum of a closed subspace and a one-dimensional subspace
       equals their join.  (Proof suggested by Eric Schechter 1-Jun-2004.) $)
    spansnji $p |- ( A +H ( span ` { B } ) ) = ( A vH ( span ` { B } ) ) $=
      ( csn cspn cfv chj co cph cort chshii spansnchi shjshsi cpj cch cun
      chssii chil wcel wss choccli pjhclii snssi ax-mp spanuni spanunsni csh
      wceq spanid opreq1i spansnpji osumi eqtr2i 3eqtr4ri 3eqtrri chjcli
      eqeltri ococi ) ABEZFGZHIAVAJIZKGKGVBAVAACLZVABDMLNVBVBABAKGZOGGZEZFGZHIZ
      PVHAUTQFGZAFGZVAJIVBAVFQFGVJVGJIZVIVHAVFACRZVESTVFSUABVDACUBDUCZVESUDUEUF
      ABCDUGVKAVGJIZVHVJAVGJAUHTVJAUIVCAUJUEZUKAVGKGUAVNVHUIABVLDULAVGCVEVMMZUM
      UEUNUOAUTVLBSTUTSUADBSUDUEUFVJAVAJVOUKUPAVGCVPUQURUSUN $.
      $( [24-Nov-2004] $) $( [1-Jun-2004] $)
  $}

  $( The subspace sum of a closed subspace and a one-dimensional subspace
     equals their join. $)
  spansnj $p |- ( ( A e. CH /\ B e. ~H ) ->
              ( A +H ( span ` { B } ) ) = ( A vH ( span ` { B } ) ) ) $=
    ( cch wcel chil csn cspn cfv cph co chj wceq cif c0v opreq1 eqeq12d sneq
    fveq2d opreq2d helch elimel ax-hv0cl spansnji dedth2h ) ACDZBEDZABFZGHZIJZA
    UHKJZLUEAEMZUHIJZUKUHKJZLUKUFBNMZFZGHZIJZUKUPKJZLABENAUKLUIULUJUMAUKUHIOAUK
    UHKOPBUNLZULUQUMURUSUHUPUKIUSUGUOGBUNQRZSUSUHUPUKKUTSPUKUNAECTUABNEUBUAUCUD
    $.
    $( [22-Dec-2004] $) $( [4-Jun-2004] $)

  $( The subspace sum of a closed subspace and a one-dimensional subspace is
     closed. $)
  spansnscl $p |- ( ( A e. CH /\ B e. ~H ) ->
              ( A +H ( span ` { B } ) ) e. CH ) $=
    ( cch wcel chil wa csn cspn cfv cph co chj spansnj chjcl spansnch sylan2
    eqeltrd ) ACDZBEDZFABGHIZJKATLKZCABMRTCDUACDSATNBOPQ $.
    $( [22-Dec-2004] $) $( [17-Dec-2004] $)

  $( The sum of two vectors belong to the span of one of them iff the other
     vector also belongs. $)
  sumspansn $p |- ( ( A e. ~H /\ B e. ~H ) ->
           ( ( A +h B ) e. ( span ` { A } ) <-> B e. ( span ` { A } ) ) ) $=
    ( chil wcel wa cva co csn cspn cfv cmv wi spansnid csh spansnsh shsubclOLD
    syl mpan2d adantr hvpncan2 eleq1d sylibd shaddclOLD mpand impbid ) ACDZBCDZ
    EZABFGZAHIJZDZBUJDZUHUKUIAKGZUJDZULUFUKUNLUGUFUKAUJDZUNAMZUFUJNDZUKUOEUNLAO
    ZUIAUJPQRSUHUMBUJABTUAUBUFULUKLUGUFUOULUKUPUFUQUOULEUKLURABUJUCQUDSUE $.
    $( [4-Feb-2006] $) $( [1-Nov-2005] $)

  ${
    $d x A $.  $d x B $.
    spansnm0.1 $e |- A e. ~H $.
    spansnm0.2 $e |- B e. ~H $.
    $( The meet of different one-dimensional subspaces is the zero subspace. $)
    spansnm0i $p |- ( -. A e. ( span ` { B } ) ->
                   ( ( span ` { A } ) i^i ( span ` { B } ) ) = 0H ) $=
      ( vx csn cspn cfv wcel wn cin c0h wss wceq cv wa c0v chil csh wi
      spansnchi chshii elspansn5 ax-mp mpanl1 ex elin elch0 3imtr4g ssrdv
      chincli chle0i sylib ) ABFGHZIJZAFGHZUNKZLMUQLNUOEUQLUOEOZUPIURUNIPZURQNZ
      URUQIURLIUOUSUTARIZUOUSUTCUNSIVAUOPUSPUTTUNBDUAZUBUNAURUCUDUEUFURUPUNUGUR
      UHUIUJUQUPUNACUAVBUKULUM $.
      $( [11-Feb-2006] $) $( [1-Nov-2005] $)
  $}

  ${
    nonbool.1 $e |- A e. ~H $.
    nonbool.2 $e |- B e. ~H $.
    nonbool.3 $e |- F = ( span ` { A } ) $.
    nonbool.4 $e |- G = ( span ` { B } ) $.
    nonbool.5 $e |- H = ( span ` { ( A +h B ) } ) $.
    $( A Hilbert lattice with two or more dimensions fails the distributive law
       and therefore cannot be a Boolean algebra.  This counterexample
       demonstrates a condition where ` ( ( H i^i F ) vH ( H i^i G ) ) = 0H `
       but ` ( H i^i ( F vH G ) ) =/= 0H ` .  The antecedent specifies that the
       vectors ` A ` and ` B ` are nonzero and non-colinear.  The last three
       hypotheses assign one-dimensional subspaces to ` F ` , ` G ` , and
       ` H ` . $)
    nonbooli $p |- ( -. ( A e. G \/ B e. F ) ->
                  ( H i^i ( F vH G ) ) =/= ( ( H i^i F ) vH ( H i^i G ) ) ) $=
      ( wcel wn wa chj co cin wceq wo wne c0h eqeq2 notbid biimparc cva csn
      cspn cfv c0v elin chil hvaddcli spansnid ax-mp eleqtrri cph csh
      spansnchi chshii eqeltri shsleji shsvai mp2an sselii mpbir2an eleq2
      mpbii elch0 sylib cch ch0 syl6eqel eleq2i wb sumspansn bitr4i sylibr
      con3i adantl spansnm0i notbii ineq12i eqeq1i 3imtr4i hvcomi eleq1i
      3bitr4ri opreqan12rd h0elch chj0i syl6eq sylanc ioran df-ne ) ADKZLZBCKZL
      ZMZECDNOZPZECPZEDPZNOZQZLZWNWPRLWTXCSWTTQZLZXCTQZXEWRXHXEXGXHXDXFXCTWTUAU
      BUCWQXGWOXFWPXFABUDOZAUEUFUGZKZWPXFXIUHXJXFXITKZXIUHQXFXIWTKZXLXMXIEKXIWS
      KXIEWSUIXIXIUEUFUGZEXIUJKXIXNKABFGUKZXIULUMJUNCDUOOZWSXICDCXJUPHXJAFUQZUR
      USZDBUEUFUGZUPIXSBGUQURUSZUTACKBDKXIXPKAXJCAUJKZAXJKFAULUMHUNBXSDBUJKZBXS
      KGBULUMIUNCDABXRXTVAVBVCVDWTTXIVEVFXIVGVHXJVIKUHXJKXQXJVJUMVKWPBXJKZXKCXJ
      BHVLYAYBXKYCVMFGABVNVBVOZVPVQVRWRXCTTNOTWQWOXATXBTNXKLXNXJPZTQWQXATQXIAXO
      FVSWPXKYDVTXAYETEXNCXJJHWAWBWCXIXSKZLXNXSPZTQWOXBTQXIBXOGVSWNYFBAUDOZXSKZ
      AXSKZYFWNYBYAYIYJVMGFBAVNVBXIYHXSABFGWDWEDXSAIVLWFVTXBYGTEXNDXSJIWAWBWCWG
      TWHWIWJWKWNWPWLWTXCWMWC $.
      $( [12-Feb-2006] $) $( [1-Nov-2005] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    spansncv.1 $e |- A e. CH $.
    spansncv.2 $e |- B e. CH $.
    spansncv.3 $e |- C e. ~H $.
    $( Hilbert space has the covering property (using spans of singletons to
       represent atoms).  Exercise 5 of [Kalmbach] p. 153. $)
    spansncvi $p |- ( ( A C. B /\ B C_ ( A vH ( span ` { C } ) ) ) ->
                   B = ( A vH ( span ` { C } ) ) ) $=
      ( vx vy vz wpss csn cspn cfv chj co wss wa pm3.27 spansnchi chlubii
      pssss adantr wi cv wcel wn wex ssel2 cva wceq wrex cph spansnji eleq2i
      chseli bitr3i cmv chil hvpncan2 cheli syl2an eleq1d csh chshii
      shsubclOLD ax-mp eleq1 biimpac sylan exp43 com14 imp45 syl5bi imp
      anandis exp45 imp41 adantrr c0v wne spansneleq mpan sseq1d spansnss
      ancoms opreq2 ax-hvaddid syl sylan9eqr eqeq2d eleq1a sylbid ex com23
      necon3bd sylan2 exp44 com12 adantrl mpd r19.23aivv sylbi anandirs
      expimpd 19.23adv pssnel syl5 pm2.43d impcom sylanc eqssd ) ABJZBACKLMZNOZ
      PZQZBXNXLXORABPZXMBPZXNBPXPAXMBDCFSZETXLXQXOABUAZUBXOXLXRXOXLXRXOXLXLXRUC
      XOXLQZGUDZBUEZYBAUEZUFZQZGUGXRXLYAYFXRGYAYCYEXRXOXLYCYEXRUCZXOYCQZXLYCQZY
      GYHYBXNUEZYIYGUCZBXNYBUHYJYBHUDZIUDZUIOZUJZIXMUKHAUKZYKYJYBAXMULOZUEYPYQX
      NYBACDFUMUNHIAXMYBDXSUOUPYOYKHIAXMYLAUEZYMXMUEZQZYOYIYEXRYTYOQZYIYEQQYMBU
      EZXRUUAYIUUBYEYRYSYOYIUUBYRYSYOYIUUBYRYSYOYIQZUUBYTYRUUCQZUUBYTYNYLUQOZBU
      EZUUBUUDYTUUEYMBYLURUEZYMURUEUUEYMUJYRYSYLYMUSYLADUTZYMXMXSUTVAVBYRYOXLYC
      UUFYCYOXLYRUUFYCYOXLYRUUFYNBUEZYLBUEZUUFYCYOQXLYRQBVCUEZUUIUUJQUUFUCBEVDZ
      YNYLBVEVFYOYCUUIYBYNBVGVHXQYRUUJXLABYLUHXTVIVAVJVKVLVMVNVOVPVQVRUUAYEUUBX
      RUCZYIYRYSYOYEUUMYSYRYOYEUUMUCUCYSYRYOYEUUMYSYMVSVTZUUMYRYOQZYEQUUNYSUUMU
      UNYSQZYMKLMZBPZXRUUBUUPUUQXMBUUNYSUUQXMUJZCURUEUUNYSUUSUCFYMCWAWBVNWCUUKU
      UBUURUULBYMWDWBVMWEUUOYEUUNUUOYDYMVSYRYOYMVSUJZYDUCYRUUTYOYDYRUUTYOYDUCYR
      UUTQZYOYBYLUJZYDUVAYNYLYBUUTYRYNYLVSUIOZYLYMVSYLUIWFYRUUGUVCYLUJUUHYLWGWH
      WIWJYRUVBYDUCUUTYLAYBWKUBWLWMWNVNWOVNWPWQWRVQWSWTVJXAXBWHVNXCXDXEGABXFXGW
      MXHXIXJXK $.
      $( [30-Jul-2008] $) $( [7-Jun-2004] $)
  $}

  $( Hilbert space has the covering property (using spans of singletons to
     represent atoms).  Exercise 5 of [Kalmbach] p. 153. $)
  spansncv $p |- ( ( A e. CH /\ B e. CH /\ C e. ~H ) ->
                  ( ( A C. B /\ B C_ ( A vH ( span ` { C } ) ) )
                 -> B = ( A vH ( span ` { C } ) ) ) ) $=
    ( cch wcel chil wpss csn cspn cfv chj co wss wa wceq wi cif c0v psseq1
    opreq1 sseq2d anbi12d eqeq2d imbi12d psseq2 sseq1 eqeq1 sneq fveq2d
    opreq2d anbi2d helch elimel ax-hv0cl spansncvi dedth3h ) ADEZBDEZCFEZABGZBA
    CHZIJZKLZMZNZBVCOZPUQAFQZBGZBVGVBKLZMZNZBVIOZPVGURBFQZGZVMVIMZNZVMVIOZPVNVM
    VGUSCRQZHZIJZKLZMZNZVMWAOZPABCFFRAVGOZVEVKVFVLWEUTVHVDVJAVGBSWEVCVIBAVGVBKT
    ZUAUBWEVCVIBWFUCUDBVMOZVKVPVLVQWGVHVNVJVOBVMVGUEBVMVIUFUBBVMVIUGUDCVROZVPWC
    VQWDWHVOWBVNWHVIWAVMWHVBVTVGKWHVAVSICVRUHUIUJZUAUKWHVIWAVMWIUCUDVGVMVRAFDUL
    UMBFDULUMCRFUNUMUOUP $.
    $( [7-Jul-2004] $) $( [9-Jun-2004] $)

