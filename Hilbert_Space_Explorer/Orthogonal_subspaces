$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Orthogonal subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    osumlem1.1 $e |- A e. CH $.
    osumlem1.2 $e |- B e. CH $.
    osumlem1.3 $e |- B (_ ( _|_ ` A ) $.
    osumlem1.4 $e |- ( ph <-> ( ( ( C e. A /\ D e. B ) /\ R = ( C +h D ) )
                 /\ ( ( x e. A /\ y e. ( _|_ ` A ) ) /\ z = ( x +h y ) ) ) ) $.
    $( Lemma for ~ osum . $)
    osumlem1 $p |- ( ph -> ( ( ( C e. H~ /\ D e. H~ ) /\
          R e. H~ ) /\ ( ( x e. H~ /\ y e. H~ ) /\ z e. H~ ) ) ) $=
      ( wcel wa cva co wceq cv cort cfv chil eleq1 hvaddclt syl5bir com12
      imdistani chel anim12i sylan choccl sylbi ) AGENZHFNZOZIGHPQZRZOZBSZENZCS
      ZETUAZNZOZDSZUSVAPQZRZOZOGUBNZHUBNZOZIUBNZOZUSUBNZVAUBNZOZVEUBNZOZOMURVMV
      HVRVKUQVMUOVKUQVLUQVKVLUQVLUPUBNVKIUPUBUCGHUDUEUFUGUMVIUNVJGEJUHHFKUHUIUJ
      VPVGVRVDVPVGVQVGVPVQVGVQVFUBNVPVEVFUBUCUSVAUDUEUFUGUTVNVCVOUSEJUHVAVBEJUK
      UHUIUJUIUL $.
      $( [19-Oct-1999] $)

    $( Lemma for ~ osum . $)
    osumlem2 $p |- ( ph -> ( ( ( normh ` ( C -h x ) ) ^ 2 ) +
                   ( ( normh ` ( D -h y ) ) ^ 2 ) ) =
                   ( ( normh ` ( R -h z ) ) ^ 2 ) ) $=
      ( wcel wa cva co wceq cv cort cfv cmv cno c2 cexp caddc pm3.27
      opreqan12d chil pm4.2 osumlem1 hvsub4t ad2ant2r syl eqtrd fveq2d opreq1d
      csp cc0 normpytht hvsubclt anim12i an4s csh wi chshi shocorth ax-mp
      shsubclt choccl sseli sylan syl2an sylc eqtr2d sylbi ) AGENZHFNZOZIGHPQZR
      ZOZBSZENZCSZETUAZNZOZDSZWCWEPQZRZOZOZGWCUBQZUCUAUDUEQHWEUBQZUCUAUDUEQUFQZ
      IWIUBQZUCUAZUDUEQZRMWMWSWNWOPQZUCUAZUDUEQZWPWMWRXAUDUEWMWQWTUCWMWQVTWJUBQ
      ZWTWBWLIVTWIWJUBVSWAUGWHWKUGUHWMGUINZHUINZOZIUINZOWCUINZWEUINZOZWIUINZOOZ
      XCWTRZWMBCDEFGHIJKLWMUJUKZXFXJXMXGXKGHWCWEULUMUNUOUPUQWNUINZWOUINZOZWNWOU
      RQUSRZXBWPRWMWNWOUTWMXLXQXNXFXJXQXGXKXDXHXEXIXQXDXHOXOXEXIOXPGWCVAHWEVAVB
      VCUMUNVSWHXRWAWKVQWDVRWGXRWNENZWOWFNZXRVQWDOZVRWGOEVDNZXSXTOXRVEEJVFZWNWO
      EVGVHYBYAXSVEYCGWCEVIVHHWFNZWGXTVRWFVDNYDWGOXTVEWFEJVJVFHWEWFVIVHFWFHLVKV
      LVMVCUMVNVOVP $.
      $( [19-Oct-1999] $)

    $( Lemma for ~ osum . $)
    osumlem3 $p |- ( ph ->
                   ( normh ` ( D -h y ) ) <_ ( normh ` ( R -h z ) ) ) $=
      ( wcel wa cva co wceq cv cort cfv cmv cno cle wbr c2 cexp cc0 caddc chil
      cr pm4.2 osumlem1 hvsubclt ad2ant2r normclt syl sqge0t 3syl w3a wb 0re
      a1i resqclt ad2ant2l 3jca leadd1t mpbid cc recnd addid2t osumlem2
      3brtr3d jca le2sqt normge0t syl2an mpbird sylbi ) AGENHFNOIGHPQROBSZENCSZ
      ETUANODSZVTWAPQROOZHWAUBQZUCUAZIWBUBQZUCUAZUDUEZMWCWHWEUFUGQZWGUFUGQZUDUE
      ZWCUHWIUIQZGVTUBQZUCUAZUFUGQZWIUIQZWIWJUDWCUHWOUDUEZWLWPUDUEZWCGUJNZHUJNZ
      OZIUJNZOVTUJNZWAUJNZOZWBUJNZOOZWNUKNZWQWCBCDEFGHIJKLWCULZUMZXGWMUJNZXHXAX
      EXKXBXFWSXCXKWTXDGVTUNUOUOZWMUPZUQWNURUSWCXGUHUKNZWOUKNZWIUKNZUTWQWRVAXJX
      GXNXOXPXNXGVBVCXGXKXHXOXLXMWNVDUSXGWDUJNZWEUKNZXPXAXEXQXBXFWTXDXQWSXCHWAU
      NVEUOZWDUPZWEVDUSZVFUHWOWIVGUSVHWCXGWIVINWLWIRXJXGWIYAVJWIVKUSWCBCDEFGHIJ
      KLXIVLVMWCXGXQWFUJNZOWHWKVAZXJXGXQYBXSXBXFYBXAXEIWBUNVEVNXRUHWEUDUEZOWGUK
      NZUHWGUDUEZOYCXQYBWEWGVOXQXRYDXTWDVPVNYBYEYFWFUPWFVPVNVQUSVRVS $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d u v w A $.  $d u v w B $.  $d u v w F $.  $d u v w G $.  $d u v w H $.
    $d u v w x $.  $d u v w y $.  $d u v w z $.
    osumlem4.1 $e |- A e. CH $.
    osumlem4.2 $e |- B e. CH $.
    osumlem4.3 $e |- B (_ ( _|_ ` A ) $.
    osumlem4.4 $e |- G e. V $.
    osumlem4.5 $e |- H e. V $.
    $( Lemma for ~ osum . $)
    osumlem4 $p |- ( ( ( ( F : NN --> A /\ G : NN --> B ) /\
                   A. w e. NN ( H ` w ) = ( ( F ` w ) +h ( G ` w ) ) )
                  /\ ( ( x e. A /\ y e. ( _|_ ` A ) ) /\ z = ( x +h y ) ) ) ->
                  ( H ~~>v z -> G ~~>v y ) ) $=
      ( vu vv cn wf wa cv cfv cva co wceq wral wcel cort chli wbr chil cc0 clt
      cle cmv cno wi wrex cr ffvrn expcom anim12d pm4.2 osumlem3 adantr
      osumlem1 lelttrt 3exp hvsubclt normclt syl ad2ant2l ad2ant2r sylc imp
      mpand exp41 com24 syl6 com4l imp41 imim2 r19.20dva exp31 r19.20
      r19.22sdv imim2d visset hlimconv syl5 wss chssi fss mpan2 ad2antlr csh
      chshi shocss ax-mp sseli anim12i jctild hlim syl6ibr ) QEGRZQFHRZSZDTZIUA
      ZXGGUAZXGHUAZUBUCUDZDQUEZSZATZEUFZBTZEUGUAZUFZSCTZXNXPUBUCUDZSZSZIXSUHUIZ
      QUJHRZXPUJUFZSZUKOTZULUIZPTXGUMUIZXJXPUNUCZUOUAZYGULUIZUPZDQUEZPQUQZUPZOU
      RUEZSHXPUHUIYBYCYQYFYBYHYIXHXSUNUCZUOUAZYGULUIZUPZDQUEZPQUQZUPZOURUEYQYCY
      BUUDYPOURYBYGURUFZSZUUCYOYHUUFUUBYNPQUUFUUAYMUPZDQUEZUUBYNUPXFXLYAUUEUUHX
      FUUEYAXLUUHXFUUEYAXLUUHUPXFUUESYASZXKUUGDQUUIXGQUFZSXKYTYLUPZUUGXFUUEYAUU
      JXKUUKUPZUUJXFUUEYAUULUUJXFXIEUFZXJFUFZSZUUEYAUULUPUPUUJXDUUMXEUUNXDUUJUU
      MQEXGGUSUTXEUUJUUNQFXGHUSUTVAUUOXKYAUUEUUKUUOXKYAUUEUUKUUOXKSYASZUUESYKYS
      UMUIZYTYLUUPUUQUUEUUPABCEFXIXJXHJKLUUPVBZVCVDUUPUUEUUQYTSYLUPZUUPXIUJUFZX
      JUJUFZSZXHUJUFZSXNUJUFZYESZXSUJUFZSSZUUEUUSUPZUUPABCEFXIXJXHJKLUURVEYKURU
      FZYSURUFZUVHUVGUVIUVJUUEUUSYKYSYGVFVGUVBUVEUVIUVCUVFUVAYEUVIUUTUVDUVAYESY
      JUJUFUVIXJXPVHYJVIVJVKVLUVCUVFUVJUVBUVEUVCUVFSYRUJUFUVJXHXSVHYRVIVJVKVMVJ
      VNVOVPVQVRVSVTYTYLYIWAVRWBWCVQVTUUAYMDQWDVJWEWFWBOPDXSINCWGWHWIXMYDYAYEXE
      YDXDXLXEFUJWJYDFKWKQFUJHWLWMWNXRYEXOXTXQUJXPEWOUFXQUJWJEJWPEWQWRWSWNWTXAO
      PDXPHMBWGXBXC $.
      $( [18-Oct-1999] $)
  $}

  ${
    $d x y w f g A $.  $d x y w f g B $.  $d x y w f g H $.
    osumlem5.1 $e |- A e. CH $.
    osumlem5.2 $e |- B e. CH $.
    $( Lemma for ~ osum . $)
    osumlem5 $p |- ( H : NN --> ( A +H B ) -> E. f E. g ( ( f : NN --> A /\
     g : NN --> B ) /\ A. w e. NN ( H ` w ) = ( ( f ` w ) +h ( g ` w ) ) ) ) $=
      ( vx vy cn cph co wf cv cfv cva wceq wrex wral wa wex wcel ffvrn ex
      chshi shsel syl6ib r19.21aiv nnex cch elisseti opreq1 eqeq2d rexbidv ac6
      opreq2 anim2i anass exbii 19.42v bitr sylibr 19.22i 3syl ) KBCLMZFNZAOZFP
      ZIOZJOZQMZRZJCSZIBSZAKTKBDOZNZVIVHVPPZVKQMZRZJCSZAKTZUAZDUBVQKCEOZNZUAVIV
      RVHWDPZQMZRZAKTZUAZEUBZDUBVGVOAKVGVHKUCZVIVFUCZVOVGWLWMKVFVHFUDUEIJBCVIBG
      UFCHUFUGUHUIVNWAAIKBDUJBUKGULVJVRRZVMVTJCWNVLVSVIVJVRVKQUMUNUOUPWCWKDWCVQ
      WEWIUAZEUBZUAZWKWBWPVQVTWHAJKCEUJCUKHULVKWFRVSWGVIVKWFVRQUQUNUPURWKVQWOUA
      ZEUBWQWJWREVQWEWIUSUTVQWOEVAVBVCVDVE $.
      $( [18-Oct-1999] $)
  $}

  ${
    $d w f g A $.  $d w f g B $.  $d w f g H $.
    $d w f g x $.  $d w f g y $.  $d w f g z $.
    osumlem6.1 $e |- A e. CH $.
    osumlem6.2 $e |- B e. CH $.
    osumlem6.3 $e |- B (_ ( _|_ ` A ) $.
    osumlem6.4 $e |- H e. V $.
    $( Lemma for ~ osum . $)
    osumlem6 $p |- ( ( ( H : NN --> ( A +H B ) /\ H ~~>v z )
                  /\ ( ( x e. A /\ y e. ( _|_ ` A ) ) /\ z = ( x +h y ) ) ) ->
                  y e. B ) $=
      ( vf vg vw cn cph co wf cv chli wbr wcel cort cfv wa cva wceq wral wex
      wi osumlem5 cch visset chlim ax-mp simplr ad2antrr osumlem4 imp sylanc
      exp31 com23 19.23aivv syl imp31 ) NDEOPFQZFCRZSTZARZDUABRZDUBUCUAUDVFVHVI
      UEPUFUDZVIEUAZVENDKRZQZNELRZQZUDMRZFUCVPVLUCVPVNUCUEPUFMNUGZUDZLUHKUHVGVJ
      VKUIUIZMDEKLFGHUJVRVSKLVRVJVGVKVRVJVGVKVOVNVISTZVKVRVJUDZVGUDEUKUAVOVTUDV
      KUIHVIVNEBULUMUNVRVOVJVGVMVOVQUOUPWAVGVTABCMDEVLVNFGHILULJUQURUSUTVAVBVCV
      D $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d x y z h A $.  $d x y z h B $.
    osumlem7.1 $e |- A e. CH $.
    osumlem7.2 $e |- B e. CH $.
    ${
      osumlem7.3 $e |- B (_ ( _|_ ` A ) $.
      $( Lemma for ~ osum . $)
      osumlem7 $p |- ( A +H B ) e. CH $=
        ( vh vz vx vy cph co cch wcel csh cn cv wf chli wbr wa wi wal
        closedsub chshi shscl cva wceq cort cfv wrex chil visset hlimvec
        pjpjtht mpan syl adantl wex shsva pm3.26 ad2antrl osumlem6 sylanc wb
        eleq1 ad2antll mpbird ex 19.23advv r2ex syl5ib mpd gen2 mpbir2an ) ABJK
        ZLMVONMOVOFPZQZVPGPZRSZTZVRVOMZUAZGUBFUBGFVOUCABACUDZBDUDZUEWBFGVTVRHPZ
        IPZUFKZUGZIAUHUIZUJHAUJZWAVSWJVQVSVRUKMZWJVRVPFULZGULUMALMWKWJCHIVRAUNU
        OUPUQVTWEAMZWFWIMZTZWHTZIURHURWAWJVTWPWAHIVTWPWAVTWPTZWAWGVOMZWMWFBMWRW
        QABWEWFWCWDUSWOWMVTWHWMWNUTVAHIGABVPCDEWLVBVCWHWAWRVDVTWOVRWGVOVEVFVGVH
        VIWHHIAWIVJVKVLVMVN $.
        $( [19-Oct-1999] $)
    $}

    $( Lemma for ~ osum . $)
    osumlem8 $p |- ( B (_ ( _|_ ` A ) -> ( A +H B ) e. CH ) $=
      ( cort cfv wss cph co cch wcel c0h cif wceq opreq1 eleq1d opreq2 h0elch
      keepel fveq2 sseq2d sseq1 chil chssi choc0 sseqtr4 elimhyp2v osumlem7
      dedth2v ) BAEFZGZABHIZJKUKALMZBHIZJKUMUKBLMZHIZJKABLLAUMNZULUNJAUMBHOPBUO
      NUNUPJBUOUMHQPUMUOUKALJCRSUKBLJDRSUKBUMEFZGUOURGLLEFZGABLLLURGUQUJURBAUME
      TUABUOURUBLUMNUSURLLUMETUALUOURUBLUCUSLRUDUEUFUGUHUI $.
      $( [19-Oct-1999] $)

    $( If two closed subspaces of a Hilbert space are orthogonal, their
       subspace sum equals their subspace join.  Lemma 3 of [Kalmbach] p. 67.
       Note that the Axiom of Choice is used for this proof (in ~ osumlem5
       and via ~ pjpjtht in ~ osumlem7 ). $)
    osum $p |- ( A (_ ( _|_ ` B ) -> ( A +H B ) = ( A vH B ) ) $=
      ( cort cfv wss cph co chj wa wceq cch wcel osumlem8 chshi shscom
      syl5eqel cun shunss chil chssi unssi shscl shssi occon2 ax-mp chjval
      eqcomi a1i ococt sseq12d mpbii syl chslej jctil eqss sylibr ) ABEFGZABHIZ
      ABJIZGZVAUTGZKUTVALUSVCVBUSUTMNZVCUSBAHIMUTBADCOABACPZBDPZQRVDABSZEFEFZUT
      EFEFZGZVCVGUTGVJABVEVFTVGUTABUAACUBBDUBUCUTABVEVFUDUEUFUGVDVHVAVIUTVHVALV
      DVAVHABCDUHUIUJUTUKULUMUNABCDUOUPUTVAUQUR $.
      $( [24-Nov-2004] $) $( [28-Oct-1999] $)

    $( Corollary of ~ osum . $)
    osumcor $p |- ( ( A i^i B ) +H ( A i^i ( _|_ ` B ) ) ) =
                  ( ( A i^i B ) vH ( A i^i ( _|_ ` B ) ) ) $=
      ( cin cort cfv wss cph co chj wceq inss2 choccl chub2 sstri chdmm3
      sseqtr4 chincl osum ax-mp ) ABEZABFGZEZFGZHUBUDIJUBUDKJLUBAFGZBKJZUEUBBUG
      ABMBUFDACNOPABCDQRUBUDABCDSAUCCBDNSTUA $.
      $( [13-Jan-2005] $) $( [5-Nov-2000] $)
  $}

  $( If two closed subspaces of a Hilbert space are orthogonal, their subspace
     sum equals their subspace join.  Lemma 3 of [Kalmbach] p. 67. $)
  osumt $p |- ( ( A e. CH /\ B e. CH /\ A (_ ( _|_ ` B ) ) ->
             ( A +H B ) = ( A vH B ) ) $=
    ( cch wcel cort cfv wss cph co chj wceq wi chil cif sseq1 opreq1 eqeq12d
    imbi12d fveq2 sseq2d opreq2 helch elimel osum dedth2h 3impia ) ACDZBCDZABEF
    ZGZABHIZABJIZKZUGUHUJUMLUGAMNZUIGZUNBHIZUNBJIZKZLUNUHBMNZEFZGZUNUSHIZUNUSJI
    ZKZLABMMAUNKZUJUOUMURAUNUIOVEUKUPULUQAUNBHPAUNBJPQRBUSKZUOVAURVDVFUIUTUNBUS
    ESTVFUPVBUQVCBUSUNHUABUSUNJUAQRUNUSAMCUBUCBMCUBUCUDUEUF $.
    $( [12-Jan-2006] $) $( [31-Oct-2005] $)

  $( The subspace sum of a closed subspace and its complement is all of
     Hilbert space. $)
  chsot $p |- ( A e. CH -> ( A +H ( _|_ ` A ) ) = H~ ) $=
    ( cch wcel cort cfv cph co chj chil wss wceq chocclt csh chsh shococss syl
    osumt mpd3an chjot eqtrd ) ABCZAADEZFGZAUBHGZIUAUBBCAUBDEJZUCUDKALUAAMCUEAN
    AOPAUBQRAST $.
    $( [17-Jan-2006] $) $( [31-Oct-2005] $)

  ${
    osumcor2.1 $e |- A e. CH $.
    osumcor2.2 $e |- B e. CH $.
    $( Corollary of ~ osum , showing it holds under the weaker hypothesis
       that ` A ` and ` B ` commute. $)
    osumcor2 $p |- ( A C_H B ->
                   ( A +H B ) = ( A vH B ) ) $=
      ( ccm wbr cph co chj wss wa wceq cort cfv cin cmcm2 choccl cmbr4 bitr
      chjcl chincl osum chjcom ineq2i opreq1i eqtr pjoml4 3eqtr eqeq2i inss1
      chshi shless ax-mp sseq1 mpbii sylbi syl chslej jctil eqss sylibr ) ABEFZ
      ABGHZABIHZJZVDVCJZKVCVDLVBVFVEVBAAMNZBMNZIHZOZVHJZVFVBAVHEFVKABCDPAVHCBDQ
      ZRSVKVJBGHZVJBIHZLZVFVJBAVICVGVHACQZVLTUAZDUBVOVMVDLZVFVNVDVMVNBAVHVGIHZO
      ZIHZBAIHVDVNVTBIHWAVJVTBIVIVSAVGVHVPVLUCUDUEVTBAVSCVHVGVLVPTUADUCUFBADCUG
      BADCUCUHUIVRVMVCJZVFVJAJWBAVIUJVJABVJVQUKACUKBDUKULUMVMVDVCUNUOUPUQUPABCD
      URUSVCVDUTVA $.
      $( [13-Jan-2005] $) $( [6-Dec-2000] $)
  $}

  ${
    spansnj.1 $e |- A e. CH $.
    spansnj.2 $e |- B e. H~ $.
    $( The subspace sum of a closed subspace and a one-dimensional subspace
       equals their join.  (Proof suggested by Eric Schechter 1-Jun-04.) $)
    spansnj $p |- ( A +H ( span ` { B } ) ) = ( A vH ( span ` { B } ) ) $=
      ( csn cspn cfv chj co cph cort chshi spansnch shjshs cpj cch cun chssi
      chil wcel wss choccl pjhcli snssi ax-mp spanun spanunsn csh wceq spanid
      opreq1i spansnpj osum eqtr2 3eqtr4r 3eqtrr chjcl eqeltr ococ ) ABEZFGZHIA
      VAJIZKGKGVBAVAACLZVABDMLNVBVBABAKGZOGGZEZFGZHIZPVHAUTQFGZAFGZVAJIVBAVFQFG
      VJVGJIZVIVHAVFACRZVESTVFSUABVDACUBDUCZVESUDUEUFABCDUGVKAVGJIZVHVJAVGJAUHT
      VJAUIVCAUJUEZUKAVGKGUAVNVHUIABVLDULAVGCVEVMMZUMUEUNUOAUTVLBSTUTSUADBSUDUE
      UFVJAVAJVOUKUPAVGCVPUQURUSUN $.
      $( [24-Nov-2004] $) $( [4-Jun-2004] $)
  $}

  $( The subspace sum of a closed subspace and a one-dimensional subspace
     equals their join. $)
  spansnjt $p |- ( ( A e. CH /\ B e. H~ ) ->
              ( A +H ( span ` { B } ) ) = ( A vH ( span ` { B } ) ) ) $=
    ( cch wcel chil csn cspn cfv cph co chj wceq cif c0v opreq1 eqeq12d sneq
    fveq2d opreq2d helch elimel ax-hv0cl spansnj dedth2h ) ACDZBEDZABFZGHZIJZAU
    HKJZLUEAEMZUHIJZUKUHKJZLUKUFBNMZFZGHZIJZUKUPKJZLABENAUKLUIULUJUMAUKUHIOAUKU
    HKOPBUNLZULUQUMURUSUHUPUKIUSUGUOGBUNQRZSUSUHUPUKKUTSPUKUNAECTUABNEUBUAUCUD
    $.
    $( [22-Dec-2004] $) $( [4-Jun-2004] $)

  $( The subspace sum of a closed subspace and a one-dimensional subspace
     is closed. $)
  spansnsclt $p |- ( ( A e. CH /\ B e. H~ ) ->
              ( A +H ( span ` { B } ) ) e. CH ) $=
    ( cch wcel chil wa csn cspn cfv cph co chj spansnjt chjclt spansncht
    sylan2 eqeltrd ) ACDZBEDZFABGHIZJKATLKZCABMRTCDUACDSATNBOPQ $.
    $( [22-Dec-2004] $) $( [17-Dec-2004] $)

  $( The sum of two vectors belong to the span of one of them iff the other
     vector also belongs. $)
  sumspansnt $p |- ( ( A e. H~ /\ B e. H~ ) ->
           ( ( A +h B ) e. ( span ` { A } ) <-> B e. ( span ` { A } ) ) ) $=
    ( chil wcel wa cva co csn cspn cfv cmv wi spansnid csh spansnsht shsubclt
    syl mpan2d adantr hvpncan2t eleq1d sylibd shaddclt mpand impbid ) ACDZBCDZE
    ZABFGZAHIJZDZBUJDZUHUKUIAKGZUJDZULUFUKUNLUGUFUKAUJDZUNAMZUFUJNDZUKUOEUNLAOZ
    UIAUJPQRSUHUMBUJABTUAUBUFULUKLUGUFUOULUKUPUFUQUOULEUKLURABUJUCQUDSUE $.
    $( [4-Feb-2006] $) $( [1-Nov-2005] $)

  ${
    $d x A $.  $d x B $.
    spansnm0.1 $e |- A e. H~ $.
    spansnm0.2 $e |- B e. H~ $.
    $( The meet of different one-dimensional subspaces is the zero subspace. $)
    spansnm0 $p |- ( -. A e. ( span ` { B } ) ->
                   ( ( span ` { A } ) i^i ( span ` { B } ) ) = 0H ) $=
      ( vx csn cspn cfv wcel wn cin c0h wss wceq cv wa c0v chil csh wi
      spansnch chshi elspansn5t ax-mp mpanl1 ex elin elch0 3imtr4g ssrdv
      chincl chle0 sylib ) ABFGHZIJZAFGHZUNKZLMUQLNUOEUQLUOEOZUPIURUNIPZURQNZUR
      UQIURLIUOUSUTARIZUOUSUTCUNSIVAUOPUSPUTTUNBDUAZUBUNAURUCUDUEUFURUPUNUGURUH
      UIUJUQUPUNACUAVBUKULUM $.
      $( [11-Feb-2006] $) $( [1-Nov-2005] $)
  $}

  ${
    nonbool.1 $e |- A e. H~ $.
    nonbool.2 $e |- B e. H~ $.
    nonbool.3 $e |- F = ( span ` { A } ) $.
    nonbool.4 $e |- G = ( span ` { B } ) $.
    nonbool.5 $e |- H = ( span ` { ( A +h B ) } ) $.
    $( A Hilbert lattice with two or more dimensions fails the distributive law
       and therefore cannot be a Boolean algebra.  This counterexample
       demonstrates a condition where ` ( ( H i^i F ) vH ( H i^i G ) ) = 0H `
       but ` ( H i^i ( F vH G ) ) =/= 0H ` .  The antecedent specifies that the
       vectors ` A ` and ` B ` are nonzero and non-colinear.  The last three
       hypotheses assign one-dimensional subspaces to ` F ` , ` G `, and
       ` H ` . $)
    nonbool $p |- ( -. ( A e. G \/ B e. F ) ->
                  ( H i^i ( F vH G ) ) =/= ( ( H i^i F ) vH ( H i^i G ) ) ) $=
      ( wcel wn wa chj co cin wceq wo wne c0h eqeq2 negbid biimparc cva csn
      cspn cfv c0v elin chil hvaddcl spansnid ax-mp eleqtrr cph csh spansnch
      chshi eqeltr shslej shsva mp2an sselii mpbir2an eleq2 mpbii elch0 sylib
      cch ch0 syl6eqel eleq2i wb sumspansnt bitr4 sylibr con3i adantl spansnm0
      negbii ineq12i eqeq1i 3imtr4 hvcom eleq1i 3bitr4r opreqan12rd h0elch
      chj0 syl6eq sylanc ioran df-ne ) ADKZLZBCKZLZMZECDNOZPZECPZEDPZNOZQZLZWNW
      PRLWTXCSWTTQZLZXCTQZXEWRXHXEXGXHXDXFXCTWTUAUBUCWQXGWOXFWPXFABUDOZAUEUFUGZ
      KZWPXFXIUHXJXFXITKZXIUHQXFXIWTKZXLXMXIEKXIWSKXIEWSUIXIXIUEUFUGZEXIUJKXIXN
      KABFGUKZXIULUMJUNCDUOOZWSXICDCXJUPHXJAFUQZURUSZDBUEUFUGZUPIXSBGUQURUSZUTA
      CKBDKXIXPKAXJCAUJKZAXJKFAULUMHUNBXSDBUJKZBXSKGBULUMIUNCDABXRXTVAVBVCVDWTT
      XIVEVFXIVGVHXJVIKUHXJKXQXJVJUMVKWPBXJKZXKCXJBHVLYAYBXKYCVMFGABVNVBVOZVPVQ
      VRWRXCTTNOTWQWOXATXBTNXKLXNXJPZTQWQXATQXIAXOFVSWPXKYDVTXAYETEXNCXJJHWAWBW
      CXIXSKZLXNXSPZTQWOXBTQXIBXOGVSWNYFBAUDOZXSKZAXSKZYFWNYBYAYIYJVMGFBAVNVBXI
      YHXSABFGWDWEDXSAIVLWFVTXBYGTEXNDXSJIWAWBWCWGTWHWIWJWKWNWPWLWTXCWMWC $.
      $( [12-Feb-2006] $) $( [1-Nov-2005] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    spansncv.1 $e |- A e. CH $.
    spansncv.2 $e |- B e. CH $.
    spansncv.3 $e |- C e. H~ $.
    $( Hilbert space has the covering property (using spans of singletons to
       represent atoms).  Exercise 5 of [Kalmbach] p. 153. $)
    spansncv $p |- ( ( A (. B /\ B (_ ( A vH ( span ` { C } ) ) ) ->
                   B = ( A vH ( span ` { C } ) ) ) $=
      ( vx vy vz wpss csn cspn cfv chj co wss wa pm3.27 spansnch chlubi pssss
      adantr wi cv wcel wn wex ssel2 cva wceq wrex cph spansnj eleq2i chsel
      bitr3 cmv chil hvpncan2t chel syl2an eleq1d csh chshi shsubclt ax-mp
      eleq1 biimpac sylan exp43 com14 imp45 syl5bi imp anandis exp45 imp41
      adantrr c0v spansneleqOLD mpan sseq1d spansnsst ancoms opreq2 ax-hvaddid
      syl sylan9eqr eqeq2d eleq1a sylbid ex com23 con3d sylan2 exp44 com12
      adantrl mpd r19.23aivv sylbi anandirs imp3a 19.23adv pssnel syl5 pm2.43d
      impcom sylanc eqssd ) ABJZBACKLMZNOZPZQZBXMXKXNRABPZXLBPZXMBPXOAXLBDCFSZE
      TXKXPXNABUAZUBXNXKXQXNXKXQXNXKXKXQUCXNXKQZGUDZBUEZYAAUEZUFZQZGUGXQXKXTYEX
      QGXTYBYDXQXTYBYDXQUCZXNXKYBYFXNYBQZXKYBQZYFYGYAXMUEZYHYFUCZBXMYAUHYIYAHUD
      ZIUDZUIOZUJZIXLUKHAUKZYJYIYAAXLULOZUEYOYPXMYAACDFUMUNHIAXLYADXRUOUPYNYJHI
      AXLYKAUEZYLXLUEZQZYNYHYDXQYSYNQZYHYDQQYLBUEZXQYTYHUUAYDYQYRYNYHUUAYQYRYNY
      HUUAYQYRYNYHQZUUAYSYQUUBQZUUAYSYMYKUQOZBUEZUUAUUCYSUUDYLBYKURUEZYLURUEUUD
      YLUJYQYRYKYLUSYKADUTZYLXLXRUTVAVBYQYNXKYBUUEYBYNXKYQUUEYBYNXKYQUUEYMBUEZY
      KBUEZUUEYBYNQXKYQQBVCUEZUUHUUIQUUEUCBEVDZYMYKBVEVFYNYBUUHYAYMBVGVHXPYQUUI
      XKABYKUHXSVIVAVJVKVLVMVNVOVPVQVRYTYDUUAXQUCZYHYQYRYNYDUULYRYQYNYDUULUCUCY
      RYQYNYDUULYRYLVSUJZUFZUULYQYNQZYDQUUNYRUULUUNYRQZYLKLMZBPZXQUUAUUPUUQXLBU
      UNYRUUQXLUJZCURUEUUNYRUUSUCFYLCVTWAVNWBUUJUUAUURUUKBYLWCWAVMWDUUOYDUUNUUO
      UUMYCYQYNUUMYCUCYQUUMYNYCYQUUMYNYCUCYQUUMQZYNYAYKUJZYCUUTYMYKYAUUMYQYMYKV
      SUIOZYKYLVSYKUIWEYQUUFUVBYKUJUUGYKWFWGWHWIYQUVAYCUCUUMYKAYAWJUBWKWLWMVNWN
      VNWOWPWQVQWRWSVJWTXAWGVNXBWLXCXDGABXEXFWLXGXHXIXJ $.
      $( [7-Jul-2004] $) $( [7-Jun-2004] $)
  $}

  $( Hilbert space has the covering property (using spans of singletons to
     represent atoms).  Exercise 5 of [Kalmbach] p. 153. $)
  spansncvt $p |- ( ( A e. CH /\ B e. CH /\ C e. H~ ) ->
                  ( ( A (. B /\ B (_ ( A vH ( span ` { C } ) ) )
                 -> B = ( A vH ( span ` { C } ) ) ) ) $=
    ( cch wcel chil wpss csn cspn cfv chj co wss wa wceq wi cif c0v psseq1
    opreq1 sseq2d anbi12d eqeq2d imbi12d psseq2 sseq1 eqeq1 sneq fveq2d
    opreq2d anbi2d helch elimel ax-hv0cl spansncv dedth3h ) ADEZBDEZCFEZABGZBAC
    HZIJZKLZMZNZBVCOZPUQAFQZBGZBVGVBKLZMZNZBVIOZPABCFFRVGURBFQZGZVMVIMZNZVMVIOZ
    PVNVMVGUSCRQZHZIJZKLZMZNZVMWAOZPAVGOZVEVKVFVLWEUTVHVDVJAVGBSWEVCVIBAVGVBKTZ
    UAUBWEVCVIBWFUCUDBVMOZVKVPVLVQWGVHVNVJVOBVMVGUEBVMVIUFUBBVMVIUGUDCVROZVPWCV
    QWDWHVOWBVNWHVIWAVMWHVBVTVGKWHVAVSICVRUHUIUJZUAUKWHVIWAVMWIUCUDVGVMVRAFDULU
    MBFDULUMCRFUNUMUOUP $.
    $( [7-Jul-2004] $) $( [9-Jun-2004] $)

