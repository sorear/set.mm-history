$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Orthogonal subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    osumlem1.1 $e |- A e. CH $.
    osumlem1.2 $e |- B e. CH $.
    osumlem1.3 $e |- B C_ ( _|_ ` A ) $.
    osumlem1.4 $e |- ( ph <-> ( ( ( C e. A /\ D e. B ) /\ R = ( C +h D ) )
                 /\ ( ( x e. A /\ y e. ( _|_ ` A ) ) /\ z = ( x +h y ) ) ) ) $.
    $( Lemma for ~ osumi . $)
    osumlem1 $p |- ( ph -> ( ( ( C e. ~H /\ D e. ~H ) /\
          R e. ~H ) /\ ( ( x e. ~H /\ y e. ~H ) /\ z e. ~H ) ) ) $=
      ( wcel wa cva cv chil cheli anim12i co wceq cort hvaddcl eleq1 syl5ibrcom
      cfv imdistani sylan choccli sylbi ) AGENZHFNZOZIGHPUAZUBZOZBQZENZCQZEUCUG
      ZNZOZDQZURUTPUAZUBZOZOGRNZHRNZOZIRNZOZURRNZUTRNZOZVDRNZOZOMUQVLVGVQUNVJUP
      VLULVHUMVIGEJSHFKSTVJUPVKVJVKUPUORNGHUDIUORUEUFUHUIVCVOVFVQUSVMVBVNUREJSU
      TVAEJUJSTVOVFVPVOVPVFVERNURUTUDVDVERUEUFUHUITUK $.
      $( [19-Oct-1999] $)

    $( Lemma for ~ osumi . $)
    osumlem2 $p |- ( ph -> ( ( ( normh ` ( C -h x ) ) ^ 2 ) +
                   ( ( normh ` ( D -h y ) ) ^ 2 ) ) =
                   ( ( normh ` ( R -h z ) ) ^ 2 ) ) $=
      ( wcel wa co wceq cfv cmv chil cva cv cort c2 cexp caddc simpr opreqan12d
      cno osumlem1 hvsub4 ad2ant2r syl eqtrd fveq2d opreq1d csp hvsubcl anim12i
      biid cc0 an4s chshii shsubcl mp3an1 sseli choccli sylan wi shocorth ax-mp
      csh syl2an normpyth sylc eqtr2d sylbi ) AGENZHFNZOZIGHUAPZQZOZBUBZENZCUBZ
      EUCRZNZOZDUBZWDWFUAPZQZOZOZGWDSPZUIRUDUEPHWFSPZUIRUDUEPUFPZIWJSPZUIRZUDUE
      PZQMWNWTWOWPUAPZUIRZUDUEPZWQWNWSXBUDUEWNWRXAUIWNWRWAWKSPZXAWCWMIWAWJWKSVT
      WBUGWIWLUGUHWNGTNZHTNZOZITNZOWDTNZWFTNZOZWJTNZOOZXDXAQZWNBCDEFGHIJKLWNUTU
      JZXGXKXNXHXLGHWDWFUKULUMUNUOUPWNWOTNZWPTNZOZWOWPUQPVAQZXCWQQWNXMXRXOXGXKX
      RXHXLXEXIXFXJXRXEXIOXPXFXJOXQGWDURHWFURUSVBULUMVTWIXSWBWLVRWEVSWHXSVRWEOW
      OENZWPWGNZXSVSWHOEVLNZVRWEXTEJVCZGWDEVDVEVSHWGNZWHYAFWGHLVFWGVLNYDWHYAWGE
      JVGVCHWFWGVDVEVHYBXTYAOXSVIYCWOWPEVJVKVMVBULWOWPVNVOVPVQ $.
      $( [19-Oct-1999] $)

    $( Lemma for ~ osumi . $)
    osumlem3 $p |- ( ph ->
                   ( normh ` ( D -h y ) ) <_ ( normh ` ( R -h z ) ) ) $=
      ( wcel wa co cle wbr cc0 chil cva wceq cv cort cfv cmv c2 cexp caddc biid
      cno cr osumlem1 hvsubcl ad2ant2r normcl syl sqge0 3syl w3a 0re a1i resqcl
      wb ad2ant2l 3jca leadd1 mpbid recnd addid2 osumlem2 3brtr3d normge0 le2sq
      cc jca syl2an mpbird sylbi ) AGENHFNOIGHUAPUBOBUCZENCUCZEUDUENODUCZVTWAUA
      PUBOOZHWAUFPZUKUEZIWBUFPZUKUEZQRZMWCWHWEUGUHPZWGUGUHPZQRZWCSWIUIPZGVTUFPZ
      UKUEZUGUHPZWIUIPZWIWJQWCSWOQRZWLWPQRZWCGTNZHTNZOZITNZOVTTNZWATNZOZWBTNZOO
      ZWNULNZWQWCBCDEFGHIJKLWCUJZUMZXGWMTNZXHXAXEXKXBXFWSXCXKWTXDGVTUNUOUOZWMUP
      ZUQWNURUSWCXGSULNZWOULNZWIULNZUTWQWRVDXJXGXNXOXPXNXGVAVBXGXKXHXOXLXMWNVCU
      SXGWDTNZWEULNZXPXAXEXQXBXFWTXDXQWSXCHWAUNVEUOZWDUPZWEVCUSZVFSWOWIVGUSVHWC
      XGWIVONWLWIUBXJXGWIYAVIWIVJUSWCBCDEFGHIJKLXIVKVLWCXGXQWFTNZOWHWKVDZXJXGXQ
      YBXSXBXFYBXAXEIWBUNVEVPXQXRSWEQRZOWGULNZSWGQRZOYCYBXQXRYDXTWDVMVPYBYEYFWF
      UPWFVMVPWEWGVNVQUSVRVS $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d u v w A $.  $d u v w B $.  $d u v w F $.  $d u v w G $.  $d u v w H $.
    $d u v w x $.  $d u v w y $.  $d u v w z $.
    osumlem4.1 $e |- A e. CH $.
    osumlem4.2 $e |- B e. CH $.
    osumlem4.3 $e |- B C_ ( _|_ ` A ) $.
    osumlem4.4 $e |- G e. _V $.
    osumlem4.5 $e |- H e. _V $.
    $( Lemma for ~ osumi . $)
    osumlem4 $p |- ( ( ( ( F : NN --> A /\ G : NN --> B ) /\
                   A. w e. NN ( H ` w ) = ( ( F ` w ) +h ( G ` w ) ) )
                  /\ ( ( x e. A /\ y e. ( _|_ ` A ) ) /\ z = ( x +h y ) ) ) ->
                  ( H ~~>v z -> G ~~>v y ) ) $=
      ( cn wa wcel wbr chil wi vu vv wf cfv cva wceq wral cort chli cc0 clt cle
      cv co cmv cno wrex cr visset hlimconvi ffvelrn expcom anim12d biid adantr
      osumlem3 osumlem1 hvsubcl normcl ad2ant2l ad2ant2r lelttr 3exp sylc mpand
      syl imp exp41 com24 com4l imp41 imim2 ralimdva exp31 ralim reximdv imim2d
      syl6 syl5 wss chssii fss mpan2 ad2antlr chshii shocss ax-mp sseli anim12i
      csh jctild hlimi syl6ibr ) OEGUCZOFHUCZPZDUMZIUDZXGGUDZXGHUDZUEUNUFZDOUGZ
      PZAUMZEQZBUMZEUHUDZQZPCUMZXNXPUEUNUFZPZPZIXSUIRZOSHUCZXPSQZPZUJUAUMZUKRZU
      BUMXGULRZXJXPUOUNZUPUDZYGUKRZTZDOUGZUBOUQZTZUAURUGZPHXPUIRYBYCYQYFYCYHYIX
      HXSUOUNZUPUDZYGUKRZTZDOUGZUBOUQZTZUAURUGYBYQUAUBDXSINCUSUTYBUUDYPUAURYBYG
      URQZPZUUCYOYHUUFUUBYNUBOUUFUUAYMTZDOUGZUUBYNTXFXLYAUUEUUHXFUUEYAXLUUHXFUU
      EYAXLUUHTXFUUEPYAPZXKUUGDOUUIXGOQZPXKYTYLTZUUGXFUUEYAUUJXKUUKTZUUJXFUUEYA
      UULUUJXFXIEQZXJFQZPZUUEYAUULTTUUJXDUUMXEUUNXDUUJUUMOEXGGVAVBXEUUJUUNOFXGH
      VAVBVCUUOXKYAUUEUUKUUOXKYAUUEUUKUUOXKPYAPZUUEPYKYSULRZYTYLUUPUUQUUEUUPABC
      EFXIXJXHJKLUUPVDZVFVEUUPUUEUUQYTPYLTZUUPXISQZXJSQZPZXHSQZPXNSQZYEPZXSSQZP
      PZUUEUUSTZUUPABCEFXIXJXHJKLUURVGUVGYKURQZYSURQZUVHUVBUVEUVIUVCUVFUVAYEUVI
      UUTUVDUVAYEPYJSQUVIXJXPVHYJVIVPVJVKUVCUVFUVJUVBUVEUVCUVFPYRSQUVJXHXSVHYRV
      IVPVJUVIUVJUUEUUSYKYSYGVLVMVNVPVQVOVRVSWHVTWAYTYLYIWBWHWCWDVSWAUUAYMDOWEV
      PWFWGWCWIXMYDYAYEXEYDXDXLXEFSWJYDFKWKOFSHWLWMWNXRYEXOXTXQSXPEWTQXQSWJEJWO
      EWPWQWRWNWSXAUAUBDXPHMBUSXBXC $.
      $( [18-Oct-1999] $)
  $}

  ${
    $d x y w f g A $.  $d x y w f g B $.  $d x y w f g H $.
    osumlem5.1 $e |- A e. CH $.
    osumlem5.2 $e |- B e. CH $.
    $( Lemma for ~ osumi . $)
    osumlem5 $p |- ( H : NN --> ( A +H B ) -> E. f E. g ( ( f : NN --> A /\
     g : NN --> B ) /\ A. w e. NN ( H ` w ) = ( ( f ` w ) +h ( g ` w ) ) ) ) $=
      ( vx vy cn co wf cv cfv cva wceq wrex wa wex cph wral wcel ffvelrn chshii
      ex shseli syl6ib r19.21aiv nnex cch elisseti opreq1 eqeq2d rexbidv opreq2
      ac6 anim2i anass exbii 19.42v bitri sylibr eximi 3syl ) KBCUALZFMZANZFOZI
      NZJNZPLZQZJCRZIBRZAKUBKBDNZMZVIVHVPOZVKPLZQZJCRZAKUBZSZDTVQKCENZMZSVIVRVH
      WDOZPLZQZAKUBZSZETZDTVGVOAKVGVHKUCZVIVFUCZVOVGWLWMKVFVHFUDUFIJBCVIBGUECHU
      EUGUHUIVNWAAIKBDUJBUKGULVJVRQZVMVTJCWNVLVSVIVJVRVKPUMUNUOUQWCWKDWCVQWEWIS
      ZETZSZWKWBWPVQVTWHAJKCEUJCUKHULVKWFQVSWGVIVKWFVRPUPUNUQURWKVQWOSZETWQWJWR
      EVQWEWIUSUTVQWOEVAVBVCVDVE $.
      $( [18-Oct-1999] $)
  $}

  ${
    $d w f g A $.  $d w f g B $.  $d w f g H $.  $d w f g x $.  $d w f g y $.
    $d w f g z $.
    osumlem6.1 $e |- A e. CH $.
    osumlem6.2 $e |- B e. CH $.
    osumlem6.3 $e |- B C_ ( _|_ ` A ) $.
    osumlem6.4 $e |- H e. _V $.
    $( Lemma for ~ osumi . $)
    osumlem6 $p |- ( ( ( H : NN --> ( A +H B ) /\ H ~~>v z )
                  /\ ( ( x e. A /\ y e. ( _|_ ` A ) ) /\ z = ( x +h y ) ) ) ->
                  y e. B ) $=
      ( vf vg vw cn co wf cv wcel cfv wa cph chli wbr cort cva wceq wral wex wi
      osumlem5 simplr ad2antrr visset osumlem4 imp chlimi mp3an1 syl11anc exp31
      cch com23 19.23aivv syl imp31 ) NDEUAOFPZFCQZUBUCZAQZDRBQZDUDSRTVFVHVIUEO
      UFTZVIERZVENDKQZPZNELQZPZTMQZFSVPVLSVPVNSUEOUFMNUGZTZLUHKUHVGVJVKUIUIZMDE
      KLFGHUJVRVSKLVRVJVGVKVRVJVGVKVRVJTZVGTVOVNVIUBUCZVKVRVOVJVGVMVOVQUKULVTVG
      WAABCMDEVLVNFGHILUMJUNUOEUTRVOWAVKHVIVNEBUMUPUQURUSVAVBVCVD $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d x y z h A $.  $d x y z h B $.
    osumlem7.1 $e |- A e. CH $.
    osumlem7.2 $e |- B e. CH $.
    ${
      osumlem7.3 $e |- B C_ ( _|_ ` A ) $.
      $( Lemma for ~ osumi . $)
      osumlem7 $p |- ( A +H B ) e. CH $=
        ( vh vz vx vy cph co cch wcel cv wa wal chshii wrex visset wex csh chli
        cn wf wbr wi isch2 shscli cva wceq cort cfv chil hlimveci pjpjth adantl
        mpan syl r2ex simprll osumlem6 shsvai syl11anc wb eleq1 ad2antll mpbird
        ex 19.23advv syl5bi mpd gen2 mpbir2an ) ABJKZLMVNUAMUCVNFNZUDZVOGNZUBUE
        ZOZVQVNMZUFZGPFPGFVNUGABACQZBDQZUHWAFGVSVQHNZINZUIKZUJZIAUKULZRHARZVTVR
        WIVPVRVQUMMZWIVQVOFSZGSUNALMWJWICHIVQAUOUQURUPWIWDAMZWEWHMZOZWGOZITHTVS
        VTWGHIAWHUSVSWOVTHIVSWOVTVSWOOZVTWFVNMZWPWLWEBMWQVSWLWMWGUTHIGABVOCDEWK
        VAABWDWEWBWCVBVCWGVTWQVDVSWNVQWFVNVEVFVGVHVIVJVKVLVM $.
        $( [19-Oct-1999] $)
    $}

    $( Lemma for ~ osumi . $)
    osumlem8 $p |- ( B C_ ( _|_ ` A ) -> ( A +H B ) e. CH ) $=
      ( cort cfv wss cph co cch wcel c0h wceq eleq1d h0elch keepel fveq2 sseq2d
      cif sseq1 opreq1 opreq2 chssii choc0 sseqtr4i elimhyp2v osumlem7 dedth2v
      chil ) BAEFZGZABHIZJKUKALSZBHIZJKUMUKBLSZHIZJKABLLAUMMZULUNJAUMBHUANBUOMU
      NUPJBUOUMHUBNUMUOUKALJCOPUKBLJDOPUKBUMEFZGUOURGLLEFZGLURGABLLUQUJURBAUMEQ
      RBUOURTLUMMUSURLLUMEQRLUOURTLUIUSLOUCUDUEUFUGUH $.
      $( [19-Oct-1999] $)

    $( If two closed subspaces of a Hilbert space are orthogonal, their
       subspace sum equals their subspace join.  Lemma 3 of [Kalmbach] p. 67.
       Note that the Axiom of Choice is used for this proof (in ~ osumlem5 and
       via ~ pjpjth in ~ osumlem7 ). $)
    osumi $p |- ( A C_ ( _|_ ` B ) -> ( A +H B ) = ( A vH B ) ) $=
      ( cort cfv wss cph chj wceq cch wcel chshii shscomi osumlem8 syl5eqel cun
      co wa chssii shunssi unssi shscli shssii occon2i ax-mp chjvali eqcomi a1i
      chil ococ sseq12d mpbii syl chsleji jctil eqss sylibr ) ABEFGZABHRZABIRZG
      ZVAUTGZSUTVAJUSVCVBUSUTKLZVCUSUTBAHRKABACMZBDMZNBADCOPVDABQZEFEFZUTEFEFZG
      ZVCVGUTGVJABVEVFUAVGUTABUJACTBDTUBUTABVEVFUCUDUEUFVDVHVAVIUTVHVAJVDVAVHAB
      CDUGUHUIUTUKULUMUNABCDUOUPUTVAUQUR $.
      $( [24-Nov-2004] $) $( [28-Oct-1999] $)

    $( Corollary of ~ osumi . $)
    osumcori $p |- ( ( A i^i B ) +H ( A i^i ( _|_ ` B ) ) ) =
                  ( ( A i^i B ) vH ( A i^i ( _|_ ` B ) ) ) $=
      ( cin cort cfv wss cph co chj inss2 choccli chub2i sstri chdmm3i sseqtr4i
      wceq chincli osumi ax-mp ) ABEZABFGZEZFGZHUBUDIJUBUDKJRUBAFGZBKJZUEUBBUGA
      BLBUFDACMNOABCDPQUBUDABCDSAUCCBDMSTUA $.
      $( [13-Jan-2005] $) $( [5-Nov-2000] $)
  $}

  $( If two closed subspaces of a Hilbert space are orthogonal, their subspace
     sum equals their subspace join.  Lemma 3 of [Kalmbach] p. 67. $)
  osum $p |- ( ( A e. CH /\ B e. CH /\ A C_ ( _|_ ` B ) ) ->
             ( A +H B ) = ( A vH B ) ) $=
    ( cch wcel cort cfv wss cph chj wceq chil cif opreq1 eqeq12d imbi12d opreq2
    co wi helch elimel sseq1 fveq2 sseq2d osumi dedth2h 3impia ) ACDZBCDZABEFZG
    ZABHQZABIQZJZUGUHUJUMRUGAKLZUIGZUNBHQZUNBIQZJZRUNUHBKLZEFZGZUNUSHQZUNUSIQZJ
    ZRABKKAUNJZUJUOUMURAUNUIUAVEUKUPULUQAUNBHMAUNBIMNOBUSJZUOVAURVDVFUIUTUNBUSE
    UBUCVFUPVBUQVCBUSUNHPBUSUNIPNOUNUSAKCSTBKCSTUDUEUF $.
    $( [31-Oct-2005] $)

  $( The subspace sum of a closed subspace and its complement is all of Hilbert
     space. $)
  chso $p |- ( A e. CH -> ( A +H ( _|_ ` A ) ) = ~H ) $=
    ( cch wcel cort cfv cph chj chil wss wceq choccl csh chsh shococss syl osum
    co mpd3an23 chjo eqtrd ) ABCZAADEZFQZAUBGQZHUAUBBCAUBDEIZUCUDJAKUAALCUEAMAN
    OAUBPRAST $.
    $( [31-Oct-2005] $)

  ${
    osumcor2.1 $e |- A e. CH $.
    osumcor2.2 $e |- B e. CH $.
    $( Corollary of ~ osumi , showing it holds under the weaker hypothesis that
       ` A ` and ` B ` commute. $)
    osumcor2i $p |- ( A C_H B ->
                   ( A +H B ) = ( A vH B ) ) $=
      ( ccm wbr cph co chj wss wceq cort cfv cin choccli chjcli chincli chjcomi
      chshii sylbi cmcm2i cmbr4i bitri osumi ineq2i opreq1i eqtri 3eqtri eqeq2i
      wa pjoml4i inss1 shlessi ax-mp sseq1 mpbii syl chsleji jctil eqss sylibr
      ) ABEFZABGHZABIHZJZVDVCJZUJVCVDKVBVFVEVBAALMZBLMZIHZNZVHJZVFVBAVHEFVKABCD
      UAAVHCBDOZUBUCVKVJBGHZVJBIHZKZVFVJBAVICVGVHACOZVLPQZDUDVOVMVDKZVFVNVDVMVN
      BAVHVGIHZNZIHZBAIHVDVNVTBIHWAVJVTBIVIVSAVGVHVPVLRUEUFVTBAVSCVHVGVLVPPQDRU
      GBADCUKBADCRUHUIVRVMVCJZVFVJAJWBAVIULVJABVJVQSACSBDSUMUNVMVDVCUOUPTUQTABC
      DURUSVCVDUTVA $.
      $( [13-Jan-2005] $) $( [6-Dec-2000] $)
  $}

  ${
    spansnj.1 $e |- A e. CH $.
    spansnj.2 $e |- B e. ~H $.
    $( The subspace sum of a closed subspace and a one-dimensional subspace
       equals their join.  (Proof suggested by Eric Schechter 1-Jun-2004.) $)
    spansnji $p |- ( A +H ( span ` { B } ) ) = ( A vH ( span ` { B } ) ) $=
      ( csn cspn cfv chj co cph cort chshii spansnchi cun chil wcel snssi ax-mp
      wss spanuni shjshsi cpj cch chssii choccli pjhclii spanunsni wceq opreq1i
      csh spanid spansnpji osumi eqtr2i 3eqtr4ri 3eqtrri chjcli eqeltri ococi )
      ABEZFGZHIAVAJIZKGKGVBAVAACLZVABDMLUAVBVBABAKGZUBGGZEZFGZHIZUCVHAUTNFGZAFG
      ZVAJIVBAVFNFGVJVGJIZVIVHAVFACUDZVEOPVFOSBVDACUEDUFZVEOQRTABCDUGVKAVGJIZVH
      VJAVGJAUJPVJAUHVCAUKRZUIAVGKGSVNVHUHABVLDULAVGCVEVMMZUMRUNUOAUTVLBOPUTOSD
      BOQRTVJAVAJVOUIUPAVGCVPUQURUSUN $.
      $( [1-Jun-2004] $)
  $}

  $( The subspace sum of a closed subspace and a one-dimensional subspace
     equals their join. $)
  spansnj $p |- ( ( A e. CH /\ B e. ~H ) ->
              ( A +H ( span ` { B } ) ) = ( A vH ( span ` { B } ) ) ) $=
    ( cch wcel chil csn cspn cfv cph co chj wceq cif opreq1 eqeq12d sneq fveq2d
    c0v opreq2d elimel helch ax-hv0cl spansnji dedth2h ) ACDZBEDZABFZGHZIJZAUHK
    JZLUEAEMZUHIJZUKUHKJZLUKUFBRMZFZGHZIJZUKUPKJZLABERAUKLUIULUJUMAUKUHINAUKUHK
    NOBUNLZULUQUMURUSUHUPUKIUSUGUOGBUNPQZSUSUHUPUKKUTSOUKUNAECUATBREUBTUCUD $.
    $( [4-Jun-2004] $)

  $( The subspace sum of a closed subspace and a one-dimensional subspace is
     closed. $)
  spansnscl $p |- ( ( A e. CH /\ B e. ~H ) ->
              ( A +H ( span ` { B } ) ) e. CH ) $=
    ( cch wcel chil wa csn cfv cph co chj spansnj spansnch chjcl sylan2 eqeltrd
    cspn ) ACDZBEDZFABGQHZIJATKJZCABLSRTCDUACDBMATNOP $.
    $( [17-Dec-2004] $)

  $( The sum of two vectors belong to the span of one of them iff the other
     vector also belongs. $)
  sumspansn $p |- ( ( A e. ~H /\ B e. ~H ) ->
           ( ( A +h B ) e. ( span ` { A } ) <-> B e. ( span ` { A } ) ) ) $=
    ( chil wcel wa cva co csn cspn cfv cmv wi csh spansnsh adantr simpr shsubcl
    spansnid syl111anc ex hvpncan2 eleq1d sylibd shaddcl 3expia syl11anc impbid
    ) ACDZBCDZEZABFGZAHIJZDZBULDZUJUMUKAKGZULDZUNUHUMUPLUIUHUMUPUHUMEULMDZUMAUL
    DZUPUHUQUMANZOUHUMPUHURUMARZOUKAULQSTOUJUOBULABUAUBUCUHUNUMLZUIUHUQURVAUSUT
    UQURUNUMABULUDUEUFOUG $.
    $( [1-Nov-2005] $)

  ${
    $d x A $.  $d x B $.
    spansnm0.1 $e |- A e. ~H $.
    spansnm0.2 $e |- B e. ~H $.
    $( The meet of different one-dimensional subspaces is the zero subspace. $)
    spansnm0i $p |- ( -. A e. ( span ` { B } ) ->
                   ( ( span ` { A } ) i^i ( span ` { B } ) ) = 0H ) $=
      ( vx csn cspn cfv wcel wn cin c0h wss wceq cv wa c0v chil csh spansnchi
      wi chshii elspansn5 ax-mp mpanl1 ex elin elch0 3imtr4g ssrdv chle0i sylib
      chincli ) ABFGHZIJZAFGHZUNKZLMUQLNUOEUQLUOEOZUPIURUNIPZURQNZURUQIURLIUOUS
      UTARIZUOUSUTCUNSIVAUOPUSPUTUAUNBDTZUBUNAURUCUDUEUFURUPUNUGURUHUIUJUQUPUNA
      CTVBUMUKUL $.
      $( [1-Nov-2005] $)
  $}

  ${
    nonbool.1 $e |- A e. ~H $.
    nonbool.2 $e |- B e. ~H $.
    nonbool.3 $e |- F = ( span ` { A } ) $.
    nonbool.4 $e |- G = ( span ` { B } ) $.
    nonbool.5 $e |- H = ( span ` { ( A +h B ) } ) $.
    $( A Hilbert lattice with two or more dimensions fails the distributive law
       and therefore cannot be a Boolean algebra.  This counterexample
       demonstrates a condition where ` ( ( H i^i F ) vH ( H i^i G ) ) = 0H `
       but ` ( H i^i ( F vH G ) ) =/= 0H ` .  The antecedent specifies that the
       vectors ` A ` and ` B ` are nonzero and non-colinear.  The last three
       hypotheses assign one-dimensional subspaces to ` F ` , ` G ` , and
       ` H ` . $)
    nonbooli $p |- ( -. ( A e. G \/ B e. F ) ->
                  ( H i^i ( F vH G ) ) =/= ( ( H i^i F ) vH ( H i^i G ) ) ) $=
      ( wcel wn chj co cin wceq c0h csn cspn ax-mp wa wne cva cfv c0v elin chil
      wo hvaddcli spansnid eleqtrri cph spansnchi chshii eqeltri shsleji shsvai
      csh mp2an sselii mpbir2an eleq2 mpbii elch0 sylib cch ch0 syl6eqel eleq2i
      wb sumspansn bitr4i sylibr adantl spansnm0i notbii ineq12i eqeq1i 3imtr4i
      con3i hvcomi eleq1i 3bitr4ri opreqan12rd h0elch chj0i syl6eq eqeq2 notbid
      biimparc syl11anc ioran df-ne ) ADKZLZBCKZLZUAZECDMNZOZECOZEDOZMNZPZLZWNW
      PUHLWTXCUBWRWTQPZLZXCQPZXEWQXGWOXFWPXFABUCNZARSUDZKZWPXFXIUEXJXFXIQKZXIUE
      PXFXIWTKZXLXMXIEKXIWSKXIEWSUFXIXIRSUDZEXIUGKXIXNKABFGUIZXIUJTJUKCDULNZWSX
      ICDCXJURHXJAFUMZUNUOZDBRSUDZURIXSBGUMUNUOZUPACKBDKXIXPKAXJCAUGKZAXJKFAUJT
      HUKBXSDBUGKZBXSKGBUJTIUKCDABXRXTUQUSUTVAWTQXIVBVCXIVDVEXJVFKUEXJKXQXJVGTV
      HWPBXJKZXKCXJBHVIYAYBXKYCVJFGABVKUSVLZVMVTVNWRXCQQMNQWQWOXAQXBQMXKLXNXJOZ
      QPWQXAQPXIAXOFVOWPXKYDVPXAYEQEXNCXJJHVQVRVSXIXSKZLXNXSOZQPWOXBQPXIBXOGVOW
      NYFBAUCNZXSKZAXSKZYFWNYBYAYIYJVJGFBAVKUSXIYHXSABFGWAWBDXSAIVIWCVPXBYGQEXN
      DXSJIVQVRVSWDQWEWFWGXHXEXGXHXDXFXCQWTWHWIWJWKWNWPWLWTXCWMVS $.
      $( [1-Nov-2005] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    spansncv.1 $e |- A e. CH $.
    spansncv.2 $e |- B e. CH $.
    spansncv.3 $e |- C e. ~H $.
    $( Hilbert space has the covering property (using spans of singletons to
       represent atoms).  Exercise 5 of [Kalmbach] p. 153. $)
    spansncvi $p |- ( ( A C. B /\ B C_ ( A vH ( span ` { C } ) ) ) ->
                   B = ( A vH ( span ` { C } ) ) ) $=
      ( vx vy vz co wss wa wi cv wcel cva wceq chil imp c0v wpss csn cspn simpr
      cfv chj pssss adantr wn wex pssnel ssel2 wrex cph eleq2i spansnchi chseli
      spansnji bitr3i cmv eleq1 biimpac sylan chshii shsubcl mp3an1 exp43 com14
      csh syl2an imp45 cheli hvpncan2 eleq1d syl5ib anandis exp45 imp41 adantrr
      wne opreq2 ax-hvaddid sylan9eqr eqeq2d eleq1a sylbid ex necon3bd spansnss
      com23 mpan spansneleq sseq1d ancoms sylan2 exp44 com12 adantrl r19.23aivv
      syl mpd sylbi anandirs expimpd 19.23adv syl5 pm2.43d impcom chlubii eqssd
      syl11anc ) ABUAZBACUBUCUEZUFJZKZLZBXNXLXOUDXPABKZXMBKZXNBKXLXQXOABUGZUHXO
      XLXRXOXLXRXOXLXLXRMXLGNZBOZXTAOZUIZLZGUJXOXLLZXRGABUKYEYDXRGYEYAYCXRXOXLY
      AYCXRMZXOYALZXLYALZYFYGXTXNOZYHYFMZBXNXTULYIXTHNZINZPJZQZIXMUMHAUMZYJYIXT
      AXMUNJZOYOYPXNXTACDFURUOHIAXMXTDCFUPZUQUSYNYJHIAXMYKAOZYLXMOZLZYNYHYCXRYT
      YNLZYHYCLLYLBOZXRUUAYHUUBYCYRYSYNYHUUBYRYSYNYHUUBYRYSYNYHLZUUBYTYRUUCLZUU
      BUUDYMYKUTJZBOZYTUUBYRYNXLYAUUFYAYNXLYRUUFYAYNXLYRUUFYAYNLYMBOZYKBOZUUFXL
      YRLYNYAUUGXTYMBVAVBXLXQYRUUHXSABYKULVCBVIOZUUGUUHUUFBEVDZYMYKBVEVFVJVGVHV
      KYTUUEYLBYRYKROZYLROUUEYLQYSYKADVLZYLXMYQVLYKYLVMVJVNVOSVPVQVRVSUUAYCUUBX
      RMZYHYRYSYNYCUUMYSYRYNYCUUMMMYSYRYNYCUUMYRYNLZYCLYSYLTVTZUUMUUNYCUUOUUNYB
      YLTYRYNYLTQZYBMYRUUPYNYBYRUUPYNYBMYRUUPLZYNXTYKQZYBUUQYMYKXTUUPYRYMYKTPJZ
      YKYLTYKPWAYRUUKUUSYKQUULYKWBWTWCWDYRUURYBMUUPYKAXTWEUHWFWGWJSWHSUUOYSUUMU
      UBYLUBUCUEZBKZUUOYSLZXRUUIUUBUVAUUJBYLWIWKUVBUUTXMBUUOYSUUTXMQZCROUUOYSUV
      CMFYLCWLWKSWMVOWNWOWPWQVRWRXAVGWSXBWTSXCXDXEXFWGXGXHAXMBDYQEXIXKXJ $.
      $( [30-Jul-2008] $) $( [7-Jun-2004] $)
  $}

  $( Hilbert space has the covering property (using spans of singletons to
     represent atoms).  Exercise 5 of [Kalmbach] p. 153. $)
  spansncv $p |- ( ( A e. CH /\ B e. CH /\ C e. ~H ) ->
                  ( ( A C. B /\ B C_ ( A vH ( span ` { C } ) ) )
                 -> B = ( A vH ( span ` { C } ) ) ) ) $=
    ( cch wcel chil wpss csn cspn cfv chj co wss wa wceq cif c0v imbi12d elimel
    wi psseq1 opreq1 sseq2d anbi12d eqeq2d psseq2 sseq1 eqeq1 sneq fveq2d helch
    opreq2d anbi2d ax-hv0cl spansncvi dedth3h ) ADEZBDEZCFEZABGZBACHZIJZKLZMZNZ
    BVCOZTUQAFPZBGZBVGVBKLZMZNZBVIOZTVGURBFPZGZVMVIMZNZVMVIOZTVNVMVGUSCQPZHZIJZ
    KLZMZNZVMWAOZTABCFFQAVGOZVEVKVFVLWEUTVHVDVJAVGBUAWEVCVIBAVGVBKUBZUCUDWEVCVI
    BWFUERBVMOZVKVPVLVQWGVHVNVJVOBVMVGUFBVMVIUGUDBVMVIUHRCVROZVPWCVQWDWHVOWBVNW
    HVIWAVMWHVBVTVGKWHVAVSICVRUIUJULZUCUMWHVIWAVMWIUERVGVMVRAFDUKSBFDUKSCQFUNSU
    OUP $.
    $( [9-Jun-2004] $)

