$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Dirac bra-ket notation (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w A $.  $d x y B $.  $d x y C $.  $d x y D $.  $d x S $.
    $d t u x y T $.  $d t u x U $.  $d t x y z $.
    $( The norm of the bra function. $)
    branmfn $p |- ( A e. ~H -> ( normfn ` ( bra ` A ) ) = ( normh ` A ) ) $=
      ( vy vx vz vw chil wcel cbr cfv cnmf cno wceq c0v fveq2 fveq2d eqeq12d
      wne wa cv c1 cle wbr cabs wrex cab cxr clt csup cc wf brafn nmfnval syl
      adantr wss wral wi cr nmfnsetre ressxr syl6ss normcl rexr jca visset
      eqeq1 anbi2d rexbidv elab csp co id bravalval sylan9eqr bcs2 3expa
      ancom1s eqbrtrd exp41 imp4a r19.23adv imp sylan2b r19.21aiva cdiv csm
      cc0 recnd normne0 biimpar reccl syl11anc simpl hvmulcl norm1 1re leidi
      syl6eqbr cmul ax-his3 syl111anc rereccl hiidrcl remulcl eqeltrd normgt0
      biimpa recgt0 0re ltle mpan sylc hiidge0 mulge0 syl22anc breqtrrd absid
      mulcom recid eqtrd opreq2d mulid1 eqtr2d mul12 c2 cexp sqval normsq
      eqtr3d 3eqtrd 3eqtr4rd breq1d opreq1 eqeq2d anbi12d rcla4ev syl12anc wb
      rexbidva mpbird fvex sylibr breq2 sylan adantlr ex supxr2 csn cxp nmfn0
      bra0 fveq2i norm0 3eqtr4i a1i pm2.61ne ) AFGZAHIZJIZAKIZLMHIZJIZMKIZLZAMA
      MLZUUSUVBUUTUVCUVEUURUVAJAMHNOAMKNPUUQAMQZRZUUSBSZKIZTUAUBZCSZUVHUURIZUCI
      ZLZRZBFUDZCUEZUFUGUHZUUTUUQUUSUVRLZUVFUUQFUIUURUJZUVSAUKZCBUURULUMUNUVGUV
      QUFUOZUUTUFGZRZDSZUUTUAUBZDUVQUPZUWEUUTUGUBZUWEESZUGUBZEUVQUDZUQZDURUPUVR
      UUTLUUQUWDUVFUUQUWBUWCUUQUVQURUFUUQUVTUVQURUOUWACBUURUSUMUTVAUUQUUTURGZUW
      CAVBZUUTVCUMVDUNUUQUWGUVFUUQUWFDUVQUWEUVQGUUQUVJUWEUVMLZRZBFUDZUWFUVPUWQC
      UWEDVEUVKUWELZUVOUWPBFUWRUVNUWOUVJUVKUWEUVMVFVGVHVIUUQUWQUWFUUQUWPUWFBFUU
      QUVHFGZUVJUWOUWFUUQUWSUVJUWOUWFUUQUWSRZUVJRZUWORUWEUVHAVJVKZUCIZUUTUAUWOU
      XAUWEUVMUXCUWOVLUWTUVMUXCLUVJUWTUVLUXBUCAUVHVMOZUNVNUXAUXCUUTUAUBZUWOUWSU
      UQUVJUXEUWSUUQUVJUXEUVHAVOVPVQUNVRVSVTWAWBWCWDUNUVGUWLDURUVGUWEURGZRUWHUW
      KUVGUWHUWKUXFUVGUUTUVQGZUWHUWKUVGUVJUUTUVMLZRZBFUDZUXGUVGUXJUVJUUTUXCLZRZ
      BFUDZUVGTUUTWEVKZAWFVKZFGZUXOKIZTUAUBZUUTUXOAVJVKZUCIZLZUXMUVGUXNUIGZUUQU
      XPUVGUUTUIGZUUTWGQZUYBUUQUYCUVFUUQUUTUWNWHZUNZUUQUYDUVFAWIWJZUUTWKWLZUUQU
      VFWMZUXNAWNWLUVGUXQTTUAAWOTWPWQWRUVGUXSUXNAAVJVKZWSVKZUXTUUTUVGUYBUUQUUQU
      XSUYKLUYHUYIUYIUXNAAWTXAZUVGUXSURGWGUXSUAUBUXTUXSLUVGUXSUYKURUYLUVGUXNURG
      ZUYJURGZUYKURGUVGUWMUYDUYMUUQUWMUVFUWNUNZUYGUUTXBWLZUUQUYNUVFAXCUNZUXNUYJ
      XDWLXEUVGWGUYKUXSUAUVGUYMWGUXNUAUBZUYNWGUYJUAUBZWGUYKUAUBUYPUVGUYMWGUXNUG
      UBZUYRUYPUVGUWMWGUUTUGUBZUYTUYOUUQUVFVUAAXFXGUUTXHWLWGURGUYMUYTUYRUQXIWGU
      XNXJXKXLUYQUUQUYSUVFAXMUNUXNUYJXNXOUYLXPUXSXQWLUVGUUTUUTUXNUUTWSVKZWSVKZU
      XNUUTUUTWSVKZWSVKZUYKUVGVUCUUTTWSVKZUUTUVGVUBTUUTWSUVGVUBUUTUXNWSVKZTUVGU
      YBUYCVUBVUGLUYHUYFUXNUUTXRWLUVGUYCUYDVUGTLUYFUYGUUTXSWLXTYAUUQVUFUUTLZUVF
      UUQUYCVUHUYEUUTYBUMUNYCUVGUYCUYBUYCVUCVUELUYFUYHUYFUUTUXNUUTYDXAUVGVUDUYJ
      UXNWSUUQVUDUYJLUVFUUQUUTYEYFVKZVUDUYJUUQUYCVUIVUDLUYEUUTYGUMAYHYIUNYAYJYK
      UXLUXRUYARBUXOFUVHUXOLZUVJUXRUXKUYAVUJUVIUXQTUAUVHUXOKNYLVUJUXCUXTUUTVUJU
      XBUXSUCUVHUXOAVJYMOYNYOYPYQUUQUXJUXMYRUVFUUQUXIUXLBFUWTUXHUXKUVJUWTUVMUXC
      UUTUXDYNVGYSUNYTUVPUXJCUUTAKUUAUVKUUTLZUVOUXIBFVUKUVNUXHUVJUVKUUTUVMVFVGV
      HVIUUBUWJUWHEUUTUVQUWIUUTUWEUGUUCYPUUDUUEUUFWDDEUVQUUTUUGYQXTUVDUUQFWGUUH
      UUIZJIWGUVBUVCUUJUVAVULJUUKUULUUMUUNUUOUUP $.
      $( [27-Jul-2009] $) $( [24-May-2006] $)

    $( The bra of a vector is a bounded functional. $)
    brabn $p |- ( A e. ~H -> ( normfn ` ( bra ` A ) ) e. RR ) $=
      ( chil wcel cbr cfv cnmf cno cr branmfn normcl eqeltrd ) ABCADEFEAGEHAIAJ
      K $.
      $( [26-May-2006] $)

    $( The set of bras equals the set of continuous linear functionals. $)
    rnbra $p |- ran bra = ( LinFn i^i ConFn ) $=
      ( vt vx vy vz cbr crn clf ccnf cin cv chil wcel csp co wceq wa copab
      wrex cnmf cfv cr braval eqeq2d simpr bralnfn adantr eqeltrd fveq2 adantl
      brabn jca ex sylbird r19.23aiv wral riesz1 biimpa hbra1 ax-17 wb ra4
      eqeq2 syl6 pm5.32d opabbid cc wf lnfnf ad2antrr fopabfv simplbi syl id
      sylan9eq syl5 reximdva mpd impbii lnfncnbd pm5.32i bitr4i ax-hilex
      opabex2 df-bra elrnopab elin 3bitr4i eqriv ) AEFZGHIZAJZBJZKLZCJZWLDJZMNZ
      OZPZBCQZOZDKRZWKGLZWKHLZPZWKWILWKWJLXAXBWKSTZUALZPZXDXAXGWTXGDKWOKLZWTWKW
      OETZOZXGXHXIWSWKBCWOUBUCXHXJXGXHXJPZXBXFXKWKXIGXHXJUDXHXIGLXJWOUEUFUGXKXE
      XISTZUAXJXEXLOXHWKXISUHUIXHXLUALXJWOUJUFUGUKULUMUNXGWLWKTZWPOZBKUOZDKRZXA
      XBXFXPBDWKUPUQXGXOWTDKXOWMWNXMOZPZBCQZWSOZXGXHPZWTXOXRWRBCXNBKURXOCUSXOWM
      XQWQXOWMXNXQWQUTXNBKVAXMWPWNVBVCVDVEYAXTWTYAXTWKXSWSYAKVFWKVGZWKXSOZXBYBX
      FXHWKVHVIYBYCXMVFLBKUOBCKVFWKVJVKVLXTVMVNULVOVPVQVRXBXCXFWKVSVTWADAKWSWKE
      BCKWPWBWCDBCAWDWEWKGHWFWGWH $.
      $( [26-May-2006] $)

    $( The bra function maps vectors one-to-one onto the set of continuous
       linear functionals. $)
    bra11 $p |- bra : ~H -1-1-onto-> ( LinFn i^i ConFn ) $=
      ( vz vt vx vy chil clf ccnf cin cbr wf1o wfn ccnv wfun crn wceq dff1o2
      cv wcel csp co wa copab ax-hilex opabex2 df-bra fnopab2 wbr wmo funcnv
      cfv wrex wi wal wb visset fnbrfvb mpan bi2anan9 eqtr3 wral fveq1 adantl
      bravalval adantlr adantr adantll 3eqtr3d exp31 com23 r19.21adv hial2eq2
      sylibd syl5 sylbird imp an4s gen2 a1i fvelrnb ax-mp cdm breldm dmopab2
      syl6eleq pm4.71ri mobii eleq1 breq1 anbi12d mo4 bitri 3imtr4i mprgbir
      rnbra mpbir3an ) EFGHZIJIEKZILMZINZWPOEWPIPABECQZERZDQZWTAQZSTZOUACDUBZIC
      DEXDUCUDZACDBUEZUFZWRXCBQZIUGZAUHZBWSABIUIXCIUJZXIOZAEUKZXCERZXJUAZXBERZX
      BXIIUGZUAZUAXCXBOZULZDUMAUMZXIWSRZXKYBXNYAADXOXQXJXRXTXOXQUAZXJXRUAZXTYDY
      EXMXBIUJZXIOZUAZXTXOXMXJXQYGXRWQXOXMXJUNXHEXCXIIBUOZUPUQWQXQYGXRUNXHEXBXI
      IYIUPUQURYHXLYFOZYDXTXLYFXIUSYDYJXDWTXBSTZOZCEUTXTYDYJYLCEYDXAYJYLYDXAYJY
      LYDXAUAZYJUAWTXLUJZWTYFUJZXDYKYJYNYOOYMWTXLYFVAVBYMYNXDOZYJXOXAYPXQXCWTVC
      VDVEYMYOYKOZYJXQXAYQXOXBWTVCVFVEVGVHVIVJCXCXBVKVLVMVNVOVPVQVRWQYCXNUNXHAE
      XIIVSVTXKXPAUHYBXJXPAXJXOXJXCIWAEXCXIIAUOWBABEXEIXFXGWCWDWEWFXPXSADXTXOXQ
      XJXRXCXBEWGXCXBXIIWHWIWJWKWLWMWNWO $.
      $( [26-May-2006] $)

    $( A bra is a continuous linear functional. $)
    bracnln $p |- ( A e. ~H -> ( bra ` A ) e. ( LinFn i^i ConFn ) ) $=
      ( chil clf ccnf cin cbr wf1o wf bra11 f1of ax-mp ffvelrni ) BCDEZAFBMFGBM
      FHIBMFJKL $.
      $( [30-May-2006] $)

    $( Value of the converse of the bra function.  Based on the Riesz Lemma
       ~ riesz4 , this very important theorem not only justifies the Dirac
       bra-ket notation, but allows us to extract a unique vector from any
       continuous linear functional from which the functional can be recovered;
       i.e. a single vector can "store" _all_ of the information contained in
       any entire continuous linear functional (mapping from ` ~H ` to
       ` CC ` ). $)
    cnvbraval $p |- ( T e. ( LinFn i^i ConFn ) ->
      ( `' bra ` T ) = U. { y e. ~H | A. x e. ~H ( T ` x ) = ( x .ih y ) } ) $=
      ( clf ccnf cin wcel cv cfv csp co wceq chil wral crab cuni cbr ccnv wa
      wrex crn rnbra eleq2i biimpri wfn wb wf wf1o bra11 f1of ax-mp ffn
      fvelrnb sylib adantr wi f1ocnvfv mpan imp opreq2d adantll bravalval
      ancoms fveq1 adantl 3eqtr2rd exp31 r19.23adv mpd r19.21aiva wreu
      f1ocnvdm riesz4 opreq2 eqeq2d ralbidv reuuni2 syl11anc mpbid eqcomd ) CDE
      FZGZAHZCIZWCBHZJKZLZAMNZBMOPZCQRIZWBWDWCWJJKZLZAMNZWIWJLZWBWLAMWBWCMGZSZW
      EQIZCLZBMTZWLWBWSWOWBCQUAZGZWSXAWBWTWACUBUCUDQMUEZXAWSUFMWAQUGZXBMWAQUHZX
      CUIMWAQUJUKMWAQULUKBMCQUMUKUNUOWPWRWLBMWPWEMGZWRWLWPXESZWRSWKWFWCWQIZWDXE
      WRWKWFLWPXEWRSWJWEWCJXEWRWJWELZXDXEWRXHUPUIMWAWECQUQURUSUTVAXFXGWFLZWRWOX
      EXIWBXEWOXIWEWCVBVCVAUOWRXGWDLXFWCWQCVDVEVFVGVHVIVJWBWJMGZWHBMVKWMWNUFXDW
      BXJUIMWACQVLURBACVMWHWMBMWJWEWJLZWGWLAMXKWFWKWDWEWJWCJVNVOVPVQVRVSVT $.
      $( [26-May-2006] $)

    $( Closure of the converse of the bra function. $)
    cnvbracl $p |- ( T e. ( LinFn i^i ConFn ) -> ( `' bra ` T ) e. ~H ) $=
      ( chil clf ccnf cin cbr wf1o wcel ccnv cfv bra11 f1ocnvdm mpan ) BCDEZFGA
      NHAFIJBHKBNAFLM $.
      $( [26-May-2006] $)

    $( The converse bra of the bra of a vector is the vector itself. $)
    cnvbrabra $p |- ( A e. ~H -> ( `' bra ` ( bra ` A ) ) = A ) $=
      ( chil clf ccnf cin cbr wf1o wcel cfv ccnv wceq bra11 f1ocnvfv1 mpan ) BC
      DEZFGABHAFIFJIAKLBOAFMN $.
      $( [30-May-2006] $)

    $( The bra of the converse bra of a continuous linear functional. $)
    bracnvbra $p |- ( T e. ( LinFn i^i ConFn ) ->
                     ( bra ` ( `' bra ` T ) ) = T ) $=
      ( chil clf ccnf cin cbr wf1o wcel ccnv cfv wceq bra11 f1ocnvfv2 mpan ) BC
      DEZFGAOHAFIJFJAKLBOAFMN $.
      $( [31-May-2006] $)

    $( The vector that a continuous linear functional is the bra of. $)
    bracnlnval $p |- ( T e. ( LinFn i^i ConFn ) ->
         T = ( bra ` U. { y e. ~H | A. x e. ~H ( T ` x ) = ( x .ih y ) } ) ) $=
      ( clf ccnf cin wcel cv cfv csp co wceq chil wral crab cuni cbr ccnv
      cnvbraval wb cnvbracl eqeltrrd wf1o bra11 f1ocnvfvb mp3an1 mpancom
      mpbird eqcomd ) CDEFZGZAHZCIULBHJKLAMNBMOPZQIZCUKUNCLZCQRIZUMLZABCSZUMMGZ
      UKUOUQTZUKUPUMMURCUAUBMUJQUCUSUKUTUDMUJUMCQUEUFUGUHUI $.
      $( [4-Jul-2008] $) $( [26-May-2006] $)

    $( Multiplication property of the converse bra function. $)
    cnvbramul $p |- ( ( A e. CC /\ T e. ( LinFn i^i ConFn ) ) ->
       ( `' bra ` ( A .fn T ) ) = ( ( * ` A ) .h ( `' bra ` T ) ) ) $=
      ( cc wcel clf ccnf cin wa ccj cfv cbr ccnv csm co chft chil wceq
      cnvbracl cjcl brafnmul sylan cjcj adantr opreq1d eqtrd sylan2 bracnvbra
      opreq2d adantl fveq2d hvmulcl syl2an cnvbrabra syl eqtr3d ) ACDZBEFGDZHZA
      IJZBKLZJZMNZKJZUTJZABONZUTJVBURVCVEUTURVCAVAKJZONZVEUQUPVAPDZVCVGQBRZUPVH
      HZVCUSIJZVFONZVGUPUSCDZVHVCVLQASZUSVATUAVJVKAVFOUPVKAQVHAUBUCUDUEUFUQVGVE
      QUPUQVFBAOBUGUHUIUEUJURVBPDZVDVBQUPVMVHVOUQVNVIUSVAUKULVBUMUNUO $.
      $( [31-May-2006] $)

    $( Dirac bra-ket associative law ` ( | A >. ` ` <. B | ) | C >. = `
       ` | A >. ( <. B | C >. ) ` i.e. the juxtaposition of an outer product
       with a ket equals a bra juxtaposed with an inner product.  Since
       ` <. B | C >. ` is a complex number, it is the first argument in the
       inner product ` .h ` that it is mapped to, although in Dirac notation it
       is placed after the ket. $)
    kbass1 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                 ( ( A ketbra B ) ` C ) = ( ( ( bra ` B ) ` C ) .h A ) ) $=
      ( chil wcel w3a ck co cfv csp csm cbr kbvalval wceq bravalval 3adant1
      opreq1d eqtr4d ) ADEZBDEZCDEZFZCABGHICBJHZAKHCBLIIZAKHABCMUBUDUCAKTUAUDUC
      NSBCOPQR $.
      $( [15-May-2006] $)

    $( Dirac bra-ket associative law ` ( <. A | B >. ) <. C | = `
       ` <. A | ( | B >. ` ` <. C | ) ` i.e. the juxtaposition of an inner
       product with a bra equals a ket juxtaposed with an outer product. $)
    kbass2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( ( bra ` A ) ` B ) .fn ( bra ` C ) ) =
                  ( ( bra ` A ) o. ( B ketbra C ) ) ) $=
      ( vx vy chil wcel w3a cbr cfv chft co cv cmul wceq wa copab csp csm csb
      ck ccom cc wf bracl brafn hfmmval syl2an 3impa hicl ancoms 3adant3
      adantr 3ad2antl3 mulcom syl11anc simp2 simp1 ax-his3 syl111anc eqtr4d
      bravalval opreq12d cvv oprex csbopr1g ax-mp csbvarg opreq1i eqtri a1i
      3eqtr4d eqeq2d pm5.32da opabbidv braval coeq1d kbval coeq2d sylan9eq
      3impb crn wss wral hvmulcl sylan an31s r19.21aiva eqid rnssopab sylib
      fopabcos syl 3adant1 eqtr2d 3eqtrd ) AFGZBFGZCFGZHZBAIJZJZCIJZKLZDMZFGZEM
      ZXBXEXCJZNLZOZPZDEQZXFXGDXECRLZBSLZXEARLZTZOZPZDEQZXABCUALZUBZWQWRWSXDXLO
      ZWQWRPXBUCGFUCXCUDYBWSABUECUFDEXBXCUGUHUIWTXKXRDEWTXFXJXQWTXFPZXIXPXGYCBA
      RLZXMNLZXNARLZXIXPYCYEXMYDNLZYFYCYDUCGZXMUCGZYEYGOWTYHXFWQWRYHWSWRWQYHBAU
      JUKULUMWSWQXFYIWRXFWSYIXECUJZUKUNZYDXMUOUPYCYIWRWQYFYGOYKWTWRXFWQWRWSUQUM
      WTWQXFWQWRWSURUMXMBAUSUTVAYCXBYDXHXMNWTXBYDOZXFWQWRYLWSABVBULUMWSWQXFXHXM
      OWRCXEVBUNVCXPYFOYCXPDXNXETZARLZYFXNVDGZXPYNOXMBSVEZDXNXEAVDRVFVGYMXNARYO
      YMXNOYPDXNVDVHVGVIVJVKVLVMVNVOWTYAXFXGXOOPDEQZXFXGXNOPDEQZUBZXSWQWRWSYAYS
      OWQWRWSPZYAYQXTUBYSWQXAYQXTDEAVPVQYTXTYRYQDEBCVRVSVTWAWRWSYSXSOZWQYTYRWBF
      WCZUUAYTXNFGZDFWDUUBYTUUCDFXFWSWRUUCXFWSPYIWRUUCYJXMBWEWFWGWHDEFFXNYRYRWI
      ZYPWJWKDEFFXOXNYQYRXEARVEYPYQWIUUDWLWMWNWOWP $.
      $( [23-May-2006] $)

    $( Dirac bra-ket associative law ` <. A | B >. ` ` <. C | D >. = `
       ` ( <. A | B >. ` ` <. C | ) | D >. ` . $)
    kbass3 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
                  ( ( ( bra ` A ) ` B ) x. ( ( bra ` C ) ` D ) ) =
         ( ( ( ( bra ` A ) ` B ) .fn ( bra ` C ) ) ` D ) ) $=
      ( chil wcel wa cbr cfv chft co cmul cc wf wceq bracl adantr brafn
      ad2antrl simprr hfmval syl111anc eqcomd ) AEFBEFGZCEFZDEFZGZGZDBAHIIZCHIZ
      JKIZUIDUJILKZUHUIMFZEMUJNZUFUKULOUDUMUGABPQUEUNUDUFCRSUDUEUFTUIDUJUAUBUC
      $.
      $( [30-May-2006] $)

    $( Dirac bra-ket associative law ` <. A | B >. ` ` <. C | D >. = `
       ` <. A | ( | B >. ` ` <. C | D >. ) ` . $)
    kbass4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
                  ( ( ( bra ` A ) ` B ) x. ( ( bra ` C ) ` D ) ) =
            ( ( bra ` A ) ` ( ( ( bra ` C ) ` D ) .h B ) ) ) $=
      ( chil wcel wa cbr cfv cmul co csm cc wceq bracl mulcom syl2an clf
      bralnfn ad2antrr adantl simplr lnfnmul syl111anc eqtr4d ) AEFZBEFZGZCEFDE
      FGZGZBAHIZIZDCHIIZJKZUMULJKZUMBLKUKIZUHULMFUMMFZUNUONUIABOCDOZULUMPQUJUKR
      FZUQUGUPUONUFUSUGUIASTUIUQUHURUAUFUGUIUBUMBUKUCUDUE $.
      $( [30-May-2006] $)

    $( Dirac bra-ket associative law ` ( | A >. ` ` <. B | ) ( | C >. `
       ` <. D | ) = ( ( | A >. ` ` <. B | ) | C >. ) <. D | ` . $)
    kbass5 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
                  ( ( A ketbra B ) o. ( C ketbra D ) ) =
                  ( ( ( A ketbra B ) ` C ) ketbra D ) ) $=
      ( vx vy chil wcel wa cv csp co csm wceq copab ccom ck cfv csb crn wss
      kbval rneqd wf kbop frn syl eqsstr3d adantl oprex eqid fopabcos cvv
      csbopr1g ax-mp csbvarg opreq1i eqtri cmul cc hicl ancoms adantll simplrl
      simpll ax-his3 syl111anc adantlll opreq1d ad2ant2lr adantr simplll
      ax-hvmulass eqtrd syl5eq eqeq2d pm5.32da opabbidv coeq12d kbvalval 3expa
      hvmulcl sylan an31s anasss 3eqtr4d ) AGHZBGHZIZCGHZDGHZIZIZEJZGHZFJZWNBKL
      ZAMLZNIEFOZWOWPWNDKLZCMLZNIEFOZPZWOWPWTCBKLZAMLZMLZNZIZEFOZABQLZCDQLZPCXJ
      RZDQLZWMXCWOWPEXAWRSZNZIZEFOZXIWMXBTZGUAZXCXQNWLXSWIWLXRXKTZGWLXKXBEFCDUB
      ZUCWLGGXKUDXTGUACDUEGGXKUFUGUHUIEFGGWRXAWSXBWQAMUJWTCMUJZWSUKXBUKULUGWMXP
      XHEFWMWOXOXGWMWOIZXNXFWPYCXNXABKLZAMLZXFXNEXAWQSZAMLZYEXAUMHZXNYGNYBEXAWQ
      AUMMUNUOYFYDAMYFEXAWNSZBKLZYDYHYFYJNYBEXAWNBUMKUNUOYIXABKYHYIXANYBEXAUMUP
      UOUQURUQURYCYEWTXDUSLZAMLZXFYCYDYKAMWHWLWOYDYKNZWGWHWLIWOIWTUTHZWJWHYMWLW
      OYNWHWKWOYNWJWOWKYNWNDVAVBVCZVCWHWJWKWOVDWHWLWOVEWTCBVFVGVHVIYCYNXDUTHZWG
      YLXFNWLWOYNWIYOVCWMYPWOWHWJYPWGWKWJWHYPCBVAZVBVJVKWGWHWLWOVLWTXDAVMVGVNVO
      VPVQVRVNWMXJWSXKXBWIXJWSNWLEFABUBVKWLXKXBNWIYAUIVSWIWJWKXMXINWIWJIZWKIZXM
      XEDQLZXIYSXLXEDQYRXLXENZWKWGWHWJUUAABCVTWAVKVIYRXEGHZWKYTXINWJWHWGUUBWJWH
      IYPWGUUBYQXDAWBWCWDEFXEDUBWCVNWEWF $.
      $( [30-May-2006] $)

    $( Dirac bra-ket associative law ` ( | A >. ` ` <. B | ) ( | C >. `
       ` <. D | ) = | A >. ` ` ( <. B | ( | C >. ` ` <. D | ) ) ` . $)
    kbass6 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
                  ( ( A ketbra B ) o. ( C ketbra D ) ) =
               ( A ketbra ( `' bra ` ( ( bra ` B ) o. ( C ketbra D ) ) ) ) ) $=
      ( chil wcel wa ck co ccom cfv csp csm cbr ccnv kbass5 wceq kbvalval
      3expa adantrr opreq1d ccj wi cc hicl kbmul syl3an1 3exp ex com13 imp43
      chft clf ccnf cin bracl bracnln cnvbramul syl2an bravalval fveq2d
      cnvbrabra opreqan12d eqtr2d anasss kbass2 3expb adantll opreq2d eqtr4d
      3eqtrd ) AEFZBEFZGZCEFZDEFZGZGZABHIZCDHIZJCVSKZDHICBLIZAMIZDHIZABNKZVTJZN
      OZKZHIZABCDPVRWAWCDHVNVOWAWCQZVPVLVMVOWJABCRSTUAVRWDAWBUBKZDMIZHIZWIVLVMV
      OVPWDWMQZVOVMVLVPWNUCZVOVMVLWOUCVOVMGZVLVPWNWPWBUDFVLVPWNCBUEWBADUFUGUHUI
      UJUKVRWHWLAHVMVQWHWLQVLVMVQGZWLCWEKZDNKZULIZWGKZWHVMVOVPWLXAQVMVOGZVPGXAW
      RUBKZWSWGKZMIZWLXBWRUDFWSUMUNUOFXAXEQVPBCUPDUQWRWSURUSXBVPXCWKXDDMXBWRWBU
      BBCUTVADVBVCVDVEWQWTWFWGVMVOVPWTWFQBCDVFVGVAVDVHVIVJVK $.
      $( [30-May-2006] $)

