$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Dirac bra-ket notation (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w A $.  $d x y B $.  $d x y C $.  $d x y D $.  $d x S $.
    $d t u x y T $.  $d t u x U $.  $d t x y z $.
    $( The norm of the bra function. $)
    branmfn $p |- ( A e. ~H -> ( normfn ` ( bra ` A ) ) = ( normh ` A ) ) $=
      ( vy vx vz vw chil wcel cbr cfv cnmf cno wceq c0v fveq2 fveq2d eqeq12d
      wne wa cv c1 cle wbr cabs wrex cab cxr clt csup cc wf brafn nmfnval syl
      adantr wss wral wi cr nmfnsetre ressxr syl6ss normcl rexr jca visset
      eqeq1 anbi2d rexbidv elab csp co id braval sylan9eqr bcs2 3expa ancom1s
      eqbrtrd exp41 imp4a r19.23adv imp sylan2b r19.21aiva cdiv csm cc0 recnd
      normne0 biimpar reccl syl11anc simpl hvmulcl norm1 1re leidi syl6eqbr
      cmul ax-his3 syl111anc rereccl hiidrcl remulcl eqeltrd normgt0 biimpa
      recgt0 0re ltle mpan sylc hiidge0 mulge0 syl22anc breqtrrd absid mulcom
      recid eqtrd opreq2d mulid1 eqtr2d mul12 c2 cexp sqval normsq eqtr3d
      3eqtrd 3eqtr4rd breq1d opreq1 eqeq2d anbi12d rcla4ev syl12anc wb
      rexbidva mpbird fvex sylibr breq2 sylan adantlr ex supxr2 csn cxp nmfn0
      bra0 fveq2i norm0 3eqtr4i a1i pm2.61ne ) AFGZAHIZJIZAKIZLMHIZJIZMKIZLZAMA
      MLZUUSUVBUUTUVCUVEUURUVAJAMHNOAMKNPUUQAMQZRZUUSBSZKIZTUAUBZCSZUVHUURIZUCI
      ZLZRZBFUDZCUEZUFUGUHZUUTUUQUUSUVRLZUVFUUQFUIUURUJZUVSAUKZCBUURULUMUNUVGUV
      QUFUOZUUTUFGZRZDSZUUTUAUBZDUVQUPZUWEUUTUGUBZUWEESZUGUBZEUVQUDZUQZDURUPUVR
      UUTLUUQUWDUVFUUQUWBUWCUUQUVQURUFUUQUVTUVQURUOUWACBUURUSUMUTVAUUQUUTURGZUW
      CAVBZUUTVCUMVDUNUUQUWGUVFUUQUWFDUVQUWEUVQGUUQUVJUWEUVMLZRZBFUDZUWFUVPUWQC
      UWEDVEUVKUWELZUVOUWPBFUWRUVNUWOUVJUVKUWEUVMVFVGVHVIUUQUWQUWFUUQUWPUWFBFUU
      QUVHFGZUVJUWOUWFUUQUWSUVJUWOUWFUUQUWSRZUVJRZUWORUWEUVHAVJVKZUCIZUUTUAUWOU
      XAUWEUVMUXCUWOVLUWTUVMUXCLUVJUWTUVLUXBUCAUVHVMOZUNVNUXAUXCUUTUAUBZUWOUWSU
      UQUVJUXEUWSUUQUVJUXEUVHAVOVPVQUNVRVSVTWAWBWCWDUNUVGUWLDURUVGUWEURGZRUWHUW
      KUVGUWHUWKUXFUVGUUTUVQGZUWHUWKUVGUVJUUTUVMLZRZBFUDZUXGUVGUXJUVJUUTUXCLZRZ
      BFUDZUVGTUUTWEVKZAWFVKZFGZUXOKIZTUAUBZUUTUXOAVJVKZUCIZLZUXMUVGUXNUIGZUUQU
      XPUVGUUTUIGZUUTWGQZUYBUUQUYCUVFUUQUUTUWNWHZUNZUUQUYDUVFAWIWJZUUTWKWLZUUQU
      VFWMZUXNAWNWLUVGUXQTTUAAWOTWPWQWRUVGUXSUXNAAVJVKZWSVKZUXTUUTUVGUYBUUQUUQU
      XSUYKLUYHUYIUYIUXNAAWTXAZUVGUXSURGWGUXSUAUBUXTUXSLUVGUXSUYKURUYLUVGUXNURG
      ZUYJURGZUYKURGUVGUWMUYDUYMUUQUWMUVFUWNUNZUYGUUTXBWLZUUQUYNUVFAXCUNZUXNUYJ
      XDWLXEUVGWGUYKUXSUAUVGUYMWGUXNUAUBZUYNWGUYJUAUBZWGUYKUAUBUYPUVGUYMWGUXNUG
      UBZUYRUYPUVGUWMWGUUTUGUBZUYTUYOUUQUVFVUAAXFXGUUTXHWLWGURGUYMUYTUYRUQXIWGU
      XNXJXKXLUYQUUQUYSUVFAXMUNUXNUYJXNXOUYLXPUXSXQWLUVGUUTUUTUXNUUTWSVKZWSVKZU
      XNUUTUUTWSVKZWSVKZUYKUVGVUCUUTTWSVKZUUTUVGVUBTUUTWSUVGVUBUUTUXNWSVKZTUVGU
      YBUYCVUBVUGLUYHUYFUXNUUTXRWLUVGUYCUYDVUGTLUYFUYGUUTXSWLXTYAUUQVUFUUTLZUVF
      UUQUYCVUHUYEUUTYBUMUNYCUVGUYCUYBUYCVUCVUELUYFUYHUYFUUTUXNUUTYDXAUVGVUDUYJ
      UXNWSUUQVUDUYJLUVFUUQUUTYEYFVKZVUDUYJUUQUYCVUIVUDLUYEUUTYGUMAYHYIUNYAYJYK
      UXLUXRUYARBUXOFUVHUXOLZUVJUXRUXKUYAVUJUVIUXQTUAUVHUXOKNYLVUJUXCUXTUUTVUJU
      XBUXSUCUVHUXOAVJYMOYNYOYPYQUUQUXJUXMYRUVFUUQUXIUXLBFUWTUXHUXKUVJUWTUVMUXC
      UUTUXDYNVGYSUNYTUVPUXJCUUTAKUUAUVKUUTLZUVOUXIBFVUKUVNUXHUVJUVKUUTUVMVFVGV
      HVIUUBUWJUWHEUUTUVQUWIUUTUWEUGUUCYPUUDUUEUUFWDDEUVQUUTUUGYQXTUVDUUQFWGUUH
      UUIZJIWGUVBUVCUUJUVAVULJUUKUULUUMUUNUUOUUP $.
      $( [27-Jul-2009] $) $( [24-May-2006] $)

    $( The bra of a vector is a bounded functional. $)
    brabn $p |- ( A e. ~H -> ( normfn ` ( bra ` A ) ) e. RR ) $=
      ( chil wcel cbr cfv cnmf cno cr branmfn normcl eqeltrd ) ABCADEFEAGEHAIAJ
      K $.
      $( [26-May-2006] $)

    $( The set of bras equals the set of continuous linear functionals. $)
    rnbra $p |- ran bra = ( LinFn i^i ConFn ) $=
      ( vt vx vy cbr crn clf ccnf cin cv wcel wa cnmf cfv cr lnfncnbd pm5.32i
      elin wceq chil wrex wfn wb csp co cmpt cvv df-bra fnmpt ax-hilex mptexg
      ax-mp a1i mprg fvelrnb bralnfn brabn jca eleq1 fveq2 eleq1d anbi12d
      syl5ibcom r19.23aiv wral riesz1 biimpa wi braval eqtr3 ex syl ralimdva
      adantl cc wf brafn lnfnf adantr ffn eqfnfv2 syl2an syl2anc sylibrd
      reximdva mpd impbii bitri 3bitr4ri eqriv ) ADEZFGHZAIZFJZWLGJZKWMWLLMZNJZ
      KZWLWKJWLWJJZWMWNWPWLOPWLFGQWRBIZDMZWLRZBSTZWQDSUAZWRXBUBCSCIZWSUCUDZUEZU
      FJZXCBSBSXFDUFBCUGUHXGWSSJZSUFJXGUICSXEUFUJUKULUMBSWLDUNUKXBWQXAWQBSXHWTF
      JZWTLMZNJZKXAWQXHXIXKWSUOWSUPUQXAXIWMXKWPWTWLFURXAXJWONWTWLLUSUTVAVBVCWQX
      DWLMZXERZCSVDZBSTZXBWMWPXOCBWLVEVFWQXNXABSWQXHKXNXDWTMZXLRZCSVDZXAXHXNXRV
      GWQXHXMXQCSXHXDSJKXPXERZXMXQVGWSXDVHXSXMXQXPXLXEVIVJVKVLVMXHSVNWTVOZSVNWL
      VOZXAXRUBZWQWSVPWMYAWPWLVQVRXTWTSUAWLSUAYBYASVNWTVSSVNWLVSCSWTWLVTWAWBWCW
      DWEWFWGWHWI $.
      $( [16-Nov-2013] $) $( [26-May-2006] $)

    $( The bra function maps vectors one-to-one onto the set of continuous
       linear functionals.  (The proof was shortened by Mario Carneiro,
       16-Nov-2013.) $)
    bra11 $p |- bra : ~H -1-1-onto-> ( LinFn i^i ConFn ) $=
      ( vx vy vz chil clf ccnf cin cbr wf1o wfn crn wceq cv cfv wi wral dff1o6
      csp co cmpt cvv wcel df-bra fnmpt ax-hilex mptexg ax-mp a1i mprg rnbra
      wa fveq1 braval adantlr adantll eqeq12d syl5ib impancom r19.21aiv ex
      hial2eq2 sylibd rgen2a mpbir3an ) DEFGZHIHDJZHKVELAMZHNZBMZHNZLZVGVILZOZB
      DPADPABDVEHQBDVIVGRSZTZUAUBZVFADADVOHUAABUCUDVPVGDUBZDUAUBVPUEBDVNUAUFUGU
      HUIUJVMABDVQVIDUBZUKZVKCMZVGRSZVTVIRSZLZCDPZVLVSVKWDVSVKUKWCCDVSVTDUBZVKW
      CVKVTVHNZVTVJNZLVSWEUKZWCVTVHVJULWHWFWAWGWBVQWEWFWALVRVGVTUMUNVRWEWGWBLVQ
      VIVTUMUOUPUQURUSUTCVGVIVAVBVCVD $.
      $( [16-Nov-2013] $) $( [26-May-2006] $)

    $( A bra is a continuous linear functional. $)
    bracnln $p |- ( A e. ~H -> ( bra ` A ) e. ( LinFn i^i ConFn ) ) $=
      ( chil clf ccnf cin cbr wf1o wf bra11 f1of ax-mp ffvelrni ) BCDEZAFBMFGBM
      FHIBMFJKL $.
      $( [30-May-2006] $)

    $( Value of the converse of the bra function.  Based on the Riesz Lemma
       ~ riesz4 , this very important theorem not only justifies the Dirac
       bra-ket notation, but allows us to extract a unique vector from any
       continuous linear functional from which the functional can be recovered;
       i.e. a single vector can "store" _all_ of the information contained in
       any entire continuous linear functional (mapping from ` ~H ` to
       ` CC ` ). $)
    cnvbraval $p |- ( T e. ( LinFn i^i ConFn ) ->
      ( `' bra ` T ) = U. { y e. ~H | A. x e. ~H ( T ` x ) = ( x .ih y ) } ) $=
      ( clf ccnf cin wcel cv cfv csp co wceq chil wral crab cuni cbr ccnv wa
      wrex crn rnbra eleq2i biimpri wfn wb wf wf1o bra11 f1of ax-mp ffn
      fvelrnb sylib adantr wi f1ocnvfv mpan imp opreq2d adantll braval ancoms
      fveq1 adantl 3eqtr2rd exp31 r19.23adv mpd r19.21aiva wreu f1ocnvdm
      riesz4 opreq2 eqeq2d ralbidv reuuni2 syl11anc mpbid eqcomd ) CDEFZGZAHZCI
      ZWCBHZJKZLZAMNZBMOPZCQRIZWBWDWCWJJKZLZAMNZWIWJLZWBWLAMWBWCMGZSZWEQIZCLZBM
      TZWLWBWSWOWBCQUAZGZWSXAWBWTWACUBUCUDQMUEZXAWSUFMWAQUGZXBMWAQUHZXCUIMWAQUJ
      UKMWAQULUKBMCQUMUKUNUOWPWRWLBMWPWEMGZWRWLWPXESZWRSWKWFWCWQIZWDXEWRWKWFLWP
      XEWRSWJWEWCJXEWRWJWELZXDXEWRXHUPUIMWAWECQUQURUSUTVAXFXGWFLZWRWOXEXIWBXEWO
      XIWEWCVBVCVAUOWRXGWDLXFWCWQCVDVEVFVGVHVIVJWBWJMGZWHBMVKWMWNUFXDWBXJUIMWAC
      QVLURBACVMWHWMBMWJWEWJLZWGWLAMXKWFWKWDWEWJWCJVNVOVPVQVRVSVT $.
      $( [26-May-2006] $)

    $( Closure of the converse of the bra function. $)
    cnvbracl $p |- ( T e. ( LinFn i^i ConFn ) -> ( `' bra ` T ) e. ~H ) $=
      ( chil clf ccnf cin cbr wf1o wcel ccnv cfv bra11 f1ocnvdm mpan ) BCDEZFGA
      NHAFIJBHKBNAFLM $.
      $( [26-May-2006] $)

    $( The converse bra of the bra of a vector is the vector itself. $)
    cnvbrabra $p |- ( A e. ~H -> ( `' bra ` ( bra ` A ) ) = A ) $=
      ( chil clf ccnf cin cbr wf1o wcel cfv ccnv wceq bra11 f1ocnvfv1 mpan ) BC
      DEZFGABHAFIFJIAKLBOAFMN $.
      $( [30-May-2006] $)

    $( The bra of the converse bra of a continuous linear functional. $)
    bracnvbra $p |- ( T e. ( LinFn i^i ConFn ) ->
                     ( bra ` ( `' bra ` T ) ) = T ) $=
      ( chil clf ccnf cin cbr wf1o wcel ccnv cfv wceq bra11 f1ocnvfv2 mpan ) BC
      DEZFGAOHAFIJFJAKLBOAFMN $.
      $( [31-May-2006] $)

    $( The vector that a continuous linear functional is the bra of. $)
    bracnlnval $p |- ( T e. ( LinFn i^i ConFn ) ->
         T = ( bra ` U. { y e. ~H | A. x e. ~H ( T ` x ) = ( x .ih y ) } ) ) $=
      ( clf ccnf cin wcel cv cfv csp co wceq chil wral crab cuni cbr ccnv
      cnvbraval wb cnvbracl eqeltrrd wf1o bra11 f1ocnvfvb mp3an1 mpancom
      mpbird eqcomd ) CDEFZGZAHZCIULBHJKLAMNBMOPZQIZCUKUNCLZCQRIZUMLZABCSZUMMGZ
      UKUOUQTZUKUPUMMURCUAUBMUJQUCUSUKUTUDMUJUMCQUEUFUGUHUI $.
      $( [4-Jul-2008] $) $( [26-May-2006] $)

    $( Multiplication property of the converse bra function. $)
    cnvbramul $p |- ( ( A e. CC /\ T e. ( LinFn i^i ConFn ) ) ->
       ( `' bra ` ( A .fn T ) ) = ( ( * ` A ) .h ( `' bra ` T ) ) ) $=
      ( cc wcel clf ccnf cin wa ccj cfv cbr ccnv csm co chft chil wceq
      cnvbracl cjcl brafnmul sylan cjcj adantr opreq1d eqtrd sylan2 bracnvbra
      opreq2d adantl fveq2d hvmulcl syl2an cnvbrabra syl eqtr3d ) ACDZBEFGDZHZA
      IJZBKLZJZMNZKJZUTJZABONZUTJVBURVCVEUTURVCAVAKJZONZVEUQUPVAPDZVCVGQBRZUPVH
      HZVCUSIJZVFONZVGUPUSCDZVHVCVLQASZUSVATUAVJVKAVFOUPVKAQVHAUBUCUDUEUFUQVGVE
      QUPUQVFBAOBUGUHUIUEUJURVBPDZVDVBQUPVMVHVOUQVNVIUSVAUKULVBUMUNUO $.
      $( [31-May-2006] $)

    $( Dirac bra-ket associative law ` ( | A >. ` ` <. B | ) | C >. = `
       ` | A >. ( <. B | C >. ) ` i.e. the juxtaposition of an outer product
       with a ket equals a bra juxtaposed with an inner product.  Since
       ` <. B | C >. ` is a complex number, it is the first argument in the
       inner product ` .h ` that it is mapped to, although in Dirac notation it
       is placed after the ket. $)
    kbass1 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                 ( ( A ketbra B ) ` C ) = ( ( ( bra ` B ) ` C ) .h A ) ) $=
      ( chil wcel w3a ck co cfv csp csm cbr kbval wceq braval 3adant1 opreq1d
      eqtr4d ) ADEZBDEZCDEZFZCABGHICBJHZAKHCBLIIZAKHABCMUBUDUCAKTUAUDUCNSBCOPQR
      $.
      $( [15-May-2006] $)

    $( Dirac bra-ket associative law ` ( <. A | B >. ) <. C | = `
       ` <. A | ( | B >. ` ` <. C | ) ` i.e. the juxtaposition of an inner
       product with a bra equals a ket juxtaposed with an outer product. $)
    kbass2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( ( bra ` A ) ` B ) .fn ( bra ` C ) ) =
                  ( ( bra ` A ) o. ( B ketbra C ) ) ) $=
      ( vx chil wcel w3a cbr cfv chft co ck ccom wceq cv wral wa csp cmul csm
      cc simpl2 simpl1 hicl syl11anc simpr simpl3 ax-mulcom ax-his3 syl111anc
      eqtr4d wf braval eqeltrd brafn syl hfmval opreq12d eqtrd wfun ffun 3syl
      kbop fvco3 kbval fveq2d hvmulcl 3eqtrd 3eqtr4d r19.21aiva wfn wb cmpt
      cvv eqid fnmpt oprex a1i mprg bracl hfmmval syl2an 3impa fneq1d mpbiri
      fco 3impb ffn eqfnfv2 mpbird ) AEFZBEFZCEFZGZBAHIZIZCHIZJKZWOBCLKZMZNZDOZ
      WRIZXBWTIZNZDEPZWNXEDEWNXBEFZQZBARKZXBCRKZSKZXJBTKZARKZXCXDXHXKXJXISKZXMX
      HXIUAFZXJUAFZXKXNNXHWLWKXOWKWLWMXGUBZWKWLWMXGUCZBAUDUEZXHXGWMXPWNXGUFZWKW
      LWMXGUGZXBCUDUEZXIXJUHUEXHXPWLWKXMXNNYBXQXRXJBAUIUJUKXHXCWPXBWQIZSKZXKXHW
      PUAFZEUAWQULZXGXCYDNXHWPXIUAXHWKWLWPXINXRXQABUMUEZXSUNXHWMYFYACUOZUPXTWPX
      BWQUQUJXHWPXIYCXJSYGXHWMXGYCXJNYAXTCXBUMUEURUSXHXDXBWSIZWOIZXLWOIZXMXHWOU
      TZEEWSULZXGXDYJNXHWKEUAWOULZYLXRAUOZEUAWOVAVBXHWLWMYMXQYABCVCZUEXTEEXBWOW
      SVDUJXHYIXLWOXHWLWMXGYIXLNXQYAXTBCXBVEUJVFXHWKXLEFZYKXMNXRXHXPWLYQYBXQXJB
      VGUEAXLUMUEVHVIVJWNWREVKZWTEVKZXAXFVLWNYRDEYDVMZEVKZYDVNFZUUADEDEYDYTVNYT
      VOVPUUBXGWPYCSVQVRVSWNEWRYTWKWLWMWRYTNZWKWLQYEYFUUCWMABVTYHDWPWQWAWBWCWDW
      EWNEUAWTULZYSWKWLWMUUDWKYNYMUUDWLWMQYOYPEEUAWOWSWFWBWGEUAWTWHUPDEWRWTWIUE
      WJ $.
      $( [16-Nov-2013] $) $( [23-May-2006] $)

    $( Dirac bra-ket associative law ` <. A | B >. ` ` <. C | D >. = `
       ` ( <. A | B >. ` ` <. C | ) | D >. ` . $)
    kbass3 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
                  ( ( ( bra ` A ) ` B ) x. ( ( bra ` C ) ` D ) ) =
         ( ( ( ( bra ` A ) ` B ) .fn ( bra ` C ) ) ` D ) ) $=
      ( chil wcel wa cbr cfv chft co cmul cc wf wceq bracl adantr brafn
      ad2antrl simprr hfmval syl111anc eqcomd ) AEFBEFGZCEFZDEFZGZGZDBAHIIZCHIZ
      JKIZUIDUJILKZUHUIMFZEMUJNZUFUKULOUDUMUGABPQUEUNUDUFCRSUDUEUFTUIDUJUAUBUC
      $.
      $( [30-May-2006] $)

    $( Dirac bra-ket associative law ` <. A | B >. ` ` <. C | D >. = `
       ` <. A | ( | B >. ` ` <. C | D >. ) ` . $)
    kbass4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
                  ( ( ( bra ` A ) ` B ) x. ( ( bra ` C ) ` D ) ) =
            ( ( bra ` A ) ` ( ( ( bra ` C ) ` D ) .h B ) ) ) $=
      ( chil wcel wa cbr cfv cmul co csm cc wceq bracl mulcom syl2an clf
      bralnfn ad2antrr adantl simplr lnfnmul syl111anc eqtr4d ) AEFZBEFZGZCEFDE
      FGZGZBAHIZIZDCHIIZJKZUMULJKZUMBLKUKIZUHULMFUMMFZUNUONUIABOCDOZULUMPQUJUKR
      FZUQUGUPUONUFUSUGUIASTUIUQUHURUAUFUGUIUBUMBUKUCUDUE $.
      $( [30-May-2006] $)

    $( Dirac bra-ket associative law ` ( | A >. ` ` <. B | ) ( | C >. `
       ` <. D | ) = ( ( | A >. ` ` <. B | ) | C >. ) <. D | ` . $)
    kbass5 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
                  ( ( A ketbra B ) o. ( C ketbra D ) ) =
                  ( ( ( A ketbra B ) ` C ) ketbra D ) ) $=
      ( vx chil wcel wa ck co ccom cfv wceq cv wral csp csm cmul cc simpr
      simplrr hicl syl11anc simplrl simpllr ax-his3 syl111anc opreq1d simplll
      ax-hvmulass eqtrd wfun wf kbop ffun syl ad2antrr ad2antlr fvco3 kbval
      3expa adantll fveq2d hvmulcl 3eqtrd ffvelrn sylan adantrr adantr opreq2d
      3eqtr4d r19.21aiva wb fco syl2an anasss wfn ffn eqfnfv2 mpbird ) AFGZBFGZ
      HZCFGZDFGZHZHZABIJZCDIJZKZCWHLZDIJZMZENZWJLZWNWLLZMZEFOZWGWQEFWGWNFGZHZWN
      DPJZCQJZBPJZAQJZXACBPJZAQJZQJZWOWPWTXDXAXERJZAQJZXGWTXCXHAQWTXASGZWDWBXCX
      HMWTWSWEXJWGWSTZWCWDWEWSUAZWNDUBUCZWCWDWEWSUDZWAWBWFWSUEZXACBUFUGUHWTXJXE
      SGZWAXIXGMXMWTWDWBXPXNXOCBUBUCWAWBWFWSUIZXAXEAUJUGUKWTWOWNWILZWHLZXBWHLZX
      DWTWHULZFFWIUMZWSWOXSMWCYAWFWSWCFFWHUMZYAABUNZFFWHUOUPUQWFYBWCWSCDUNZURXK
      FFWNWHWIUSUGWTXRXBWHWFWSXRXBMZWCWDWEWSYFCDWNUTVAVBVCWTWAWBXBFGZXTXDMXQXOW
      TXJWDYGXMXNXACVDUCABXBUTUGVEWTWPXAWKQJZXGWTWKFGZWEWSWPYHMWGYIWSWCWDYIWEWC
      YCWDYIYDFFCWHVFVGZVHVIXLXKWKDWNUTUGWTWKXFXAQWTWAWBWDWKXFMXQXOXNABCUTUGVJU
      KVKVLWGFFWJUMZFFWLUMZWMWRVMZWCYCYBYKWFYDYEFFFWHWIVNVOWCWDWEYLWCWDHYIWEYLY
      JWKDUNVGVPYKWJFVQWLFVQYMYLFFWJVRFFWLVREFWJWLVSVOUCVT $.
      $( [16-Nov-2013] $) $( [30-May-2006] $)

    $( Dirac bra-ket associative law ` ( | A >. ` ` <. B | ) ( | C >. `
       ` <. D | ) = | A >. ` ` ( <. B | ( | C >. ` ` <. D | ) ) ` . $)
    kbass6 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
                  ( ( A ketbra B ) o. ( C ketbra D ) ) =
               ( A ketbra ( `' bra ` ( ( bra ` B ) o. ( C ketbra D ) ) ) ) ) $=
      ( chil wcel wa ck co ccom cfv csp csm cbr ccnv kbass5 wceq kbval 3expa
      adantrr opreq1d ccj wi cc hicl kbmul syl3an1 3exp ex com13 imp43 chft
      clf ccnf cin bracl bracnln cnvbramul syl2an braval fveq2d cnvbrabra
      opreqan12d eqtr2d anasss kbass2 3expb adantll opreq2d eqtr4d 3eqtrd ) AEF
      ZBEFZGZCEFZDEFZGZGZABHIZCDHIZJCVSKZDHICBLIZAMIZDHIZABNKZVTJZNOZKZHIZABCDP
      VRWAWCDHVNVOWAWCQZVPVLVMVOWJABCRSTUAVRWDAWBUBKZDMIZHIZWIVLVMVOVPWDWMQZVOV
      MVLVPWNUCZVOVMVLWOUCVOVMGZVLVPWNWPWBUDFVLVPWNCBUEWBADUFUGUHUIUJUKVRWHWLAH
      VMVQWHWLQVLVMVQGZWLCWEKZDNKZULIZWGKZWHVMVOVPWLXAQVMVOGZVPGXAWRUBKZWSWGKZM
      IZWLXBWRUDFWSUMUNUOFXAXEQVPBCUPDUQWRWSURUSXBVPXCWKXDDMXBWRWBUBBCUTVADVBVC
      VDVEWQWTWFWGVMVOVPWTWFQBCDVFVGVAVDVHVIVJVK $.
      $( [30-May-2006] $)

