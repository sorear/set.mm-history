$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Dirac bra-ket notation (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w A $.  $d x y B $.  $d x y C $.  $d x y D $.  $d x S $.
    $d t u x y T $.  $d t u x U $.  $d t x y z $.
    $( The norm of the bra function. $)
    branmfnt $p |- ( A e. H~ -> ( normfn ` ( bra ` A ) ) = ( normh ` A ) ) $=
      ( vy vx vz vw chil wcel cbr cfv cnmf cno wceq c0v fveq2 fveq2d eqeq12d
      wne wa cv c1 cle wbr cabs wrex cab cxr clt csup cc wf brafnt nmfnvalt
      syl adantr wss wral wi cr supxr2 nmfnsetret ressxr a1i sstrd normclt
      rexrt jca csp co id bravalvalt sylan9eqr bcs2t 3expa ancom1s eqbrtrd
      exp41 imp4a r19.23adv imp visset eqeq1 anbi2d rexbidv elab sylan2b
      r19.21aiva breq2 rcla4ev cdiv csm breq1d opreq1 eqeq2d anbi12d hvmulclt
      cc0 recclt recnd normne0t biimpar sylanc pm3.26 norm1t 1re leid syl6eqbr
      cmul ax-his3 syl3anc absidt axmulrcl rerecclt hiidrclt eqeltrd mulge0t
      0re ltlet mpan recgt0t normgt0t biimpa sylc hiidge0t syl2anc breqtrrd
      axmulcom recidt eqtrd opreq2d ax1id eqtr2d mul12t c2 cexp sqvalt normsqt
      eqtr3d 3eqtrd 3eqtr4rd wb rexbidva mpbird fvex sylibr sylan adantlr ex
      csn cxp nmfn0 bra0 fveq2i norm0 3eqtr4 pm2.61ne ) AFGZAHIZJIZAKIZLMHIZJIZ
      MKIZLZAMAMLZUURUVAUUSUVBUVDUUQUUTJAMHNOAMKNPUUPAMQZRZUURBSZKIZTUAUBZCSZUV
      GUUQIZUCIZLZRZBFUDZCUEZUFUGUHZUUSUUPUURUVQLZUVEUUPFUIUUQUJZUVRAUKZCBUUQUL
      UMUNUVPUFUOZUUSUFGZRZDSZUUSUAUBZDUVPUPZUWDUUSUGUBZUWDESZUGUBZEUVPUDZUQZDU
      RUPZRUVQUUSLUVFDEUVPUUSUSUUPUWCUVEUUPUWAUWBUUPUVPURUFUUPUVSUVPURUOUVTCBUU
      QUTUMURUFUOUUPVAVBVCUUPUUSURGZUWBAVDZUUSVEUMVFUNUVFUWFUWLUUPUWFUVEUUPUWED
      UVPUUPUVIUWDUVLLZRZBFUDZUWEUWDUVPGUUPUWQUWEUUPUWPUWEBFUUPUVGFGZUVIUWOUWEU
      UPUWRUVIUWOUWEUUPUWRRZUVIRZUWORUWDUVGAVGVHZUCIZUUSUAUWOUWTUWDUVLUXBUWOVIU
      WSUVLUXBLUVIUWSUVKUXAUCAUVGVJOZUNVKUWTUXBUUSUAUBZUWOUWRUUPUVIUXDUWRUUPUVI
      UXDUVGAVLVMVNUNVOVPVQVRVSUVOUWQCUWDDVTUVJUWDLZUVNUWPBFUXEUVMUWOUVIUVJUWDU
      VLWAWBWCWDWEWFUNUVFUWKDURUVFUWDURGZRUWGUWJUVFUWGUWJUXFUUSUVPGZUWGUWJUVFUW
      IUWGEUUSUVPUWHUUSUWDUGWGWHUVFUVIUUSUVLLZRZBFUDZUXGUVFUXJUVIUUSUXBLZRZBFUD
      ZTUUSWIVHZAWJVHZFGZUXOKIZTUAUBZUUSUXOAVGVHZUCIZLZRZUXMUVFUXLUYBBUXOFUVGUX
      OLZUVIUXRUXKUYAUYCUVHUXQTUAUVGUXOKNWKUYCUXBUXTUUSUYCUXAUXSUCUVGUXOAVGWLOW
      MWNWHUXNUIGZUUPUXPUVFUXNAWOUUSUIGZUUSWPQZUYDUVFUUSWQUUPUYEUVEUUPUUSUWNWRZ
      UNZUUPUYFUVEAWSWTZXAZUUPUVEXBZXAUVFUXRUYAUVFUXQTTUAAXCTXDXEXFUVFUXSUXNAAV
      GVHZXGVHZUXTUUSUYDUUPUUPUXSUYMLUVFUXNAAXHUYJUYKUYKXIZUXSURGWPUXSUAUBUXTUX
      SLUVFUXSXJUVFUXSUYMURUYNUXNURGZUYLURGZUYMURGUVFUXNUYLXKUWMUYFUYOUVFUUSXLU
      UPUWMUVEUWNUNZUYIXAZUUPUYPUVEAXMUNZXAXNUVFWPUYMUXSUAUYOUYPWPUXNUAUBZWPUYL
      UAUBZWPUYMUAUBUVFUXNUYLXOUYRUYSUYOWPUXNUGUBZUYTUVFWPURGUYOVUBUYTUQXPWPUXN
      XQXRUYRUWMWPUUSUGUBZVUBUVFUUSXSUYQUUPUVEVUCAXTYAXAYBUUPVUAUVEAYCUNYDUYNYE
      XAUVFUUSUUSUXNUUSXGVHZXGVHZUXNUUSUUSXGVHZXGVHZUYMUVFVUEUUSTXGVHZUUSUVFVUD
      TUUSXGUVFVUDUUSUXNXGVHZTUYDUYEVUDVUILUVFUXNUUSYFUYJUYHXAUYEUYFVUITLUVFUUS
      YGUYHUYIXAYHYIUUPVUHUUSLZUVEUUPUYEVUJUYGUUSYJUMUNYKUYEUYDUYEVUEVUGLUVFUUS
      UXNUUSYLUYHUYJUYHXIUVFVUFUYLUXNXGUUPVUFUYLLUVEUUPUUSYMYNVHZVUFUYLUUPUYEVU
      KVUFLUYGUUSYOUMAYPYQUNYIYRYSVFXAUUPUXJUXMYTUVEUUPUXIUXLBFUWSUXHUXKUVIUWSU
      VLUXBUUSUXCWMWBUUAUNUUBUVOUXJCUUSAKUUCUVJUUSLZUVNUXIBFVULUVMUXHUVIUVJUUSU
      VLWAWBWCWDUUDUUEUUFUUGWFVFXAYHUVCUUPFWPUUHUUIZJIWPUVAUVBUUJUUTVUMJUUKUULU
      UMUUNVBUUO $.
      $( [30-Jun-2006] $) $( [24-May-2006] $)

    $( The bra of a vector is a bounded functional. $)
    brabnt $p |- ( A e. H~ -> ( normfn ` ( bra ` A ) ) e. RR ) $=
      ( chil wcel cbr cfv cnmf cno cr branmfnt normclt eqeltrd ) ABCADEFEAGEHAI
      AJK $.
      $( [4-Jul-2006] $) $( [26-May-2006] $)

    $( The set of bras equals the set of continuous linear functionals. $)
    rnbra $p |- ran bra = ( LinFn i^i ConFn ) $=
      ( vt vx vy vz cbr crn clf ccnf cin cv chil wcel csp co wceq wa copab
      wrex cnmf cfv cr bravalt eqeq2d pm3.27 bralnfnt adantr eqeltrd fveq2
      adantl brabnt jca ex sylbird r19.23aiv wral riesz1t biimpa cc wf lnfnft
      ad2antrr fopabfv pm3.26bi syl id sylan9eq hbra1 ax-17 wb ra4 eqeq2 syl6
      pm5.32d opabbid syl5 r19.22dva mpd impbi lnfncnbdt pm5.32i bitr4
      ax-hilex opabex2 df-bra elrnopab elin 3bitr4 eqriv ) AEFZGHIZAJZBJZKLZCJZ
      WLDJZMNZOZPZBCQZOZDKRZWKGLZWKHLZPZWKWILWKWJLXAXBWKSTZUALZPZXDXAXGWTXGDKWO
      KLZWTWKWOETZOZXGXHXIWSWKBCWOUBUCXHXJXGXHXJPZXBXFXKWKXIGXHXJUDXHXIGLXJWOUE
      UFUGXKXEXISTZUAXJXEXLOXHWKXISUHUIXHXLUALXJWOUJUFUGUKULUMUNXGWLWKTZWPOZBKU
      OZDKRZXAXBXFXPBDWKUPUQXGXOWTDKXGXHPZWMWNXMOZPZBCQZWSOZWTXOXQYAWTXQYAWKXTW
      SXQKURWKUSZWKXTOZXBYBXFXHWKUTVAYBYCXMURLBKUOBCKURWKVBVCVDYAVEVFULXOXSWRBC
      XNBKVGXOCVHXOWMXRWQXOWMXNXRWQVIXNBKVJXMWPWNVKVLVMVNVOVPVQVRXBXCXFWKVSVTWA
      DAKWSWKEBCKWPWBWCDBCAWDWEWKGHWFWGWH $.
      $( [5-Jul-2006] $) $( [26-May-2006] $)

    $( The bra function maps vectors one-to-one onto the set of continuous
       linear functionals. $)
    bra11 $p |- bra : H~ -1-1-onto-> ( LinFn i^i ConFn ) $=
      ( vz vt vx vy chil clf ccnf cin cbr wf1o wfn ccnv wfun crn wceq w3a cv
      wcel csp co wa copab ax-hilex opabex2 df-bra fnopab2 wbr wmo funcnv cfv
      wrex wi wal wb visset fnbrfvb mpan bi2anan9 wral fveq1 adantl bravalvalt
      adantlr adantr adantll 3eqtr3d exp31 com23 r19.21adv hial2eq2t sylibd
      eqtr3t syl5 sylbird imp an4s gen2 a1i fvelrnb ax-mp cdm breldm dmopab2
      syl6eleq pm4.71ri mobii eleq1 breq1 anbi12d mo4 bitr 3imtr4 mprgbir
      rnbra 3pm3.2i f1o2 mpbir ) EFGHZIJIEKZILMZINZWROZPWSWTXBABECQZERZDQZXCAQZ
      STZOUACDUBZICDEXGUCUDZACDBUEZUFZWTXFBQZIUGZAUHZBXAABIUIXFIUJZXLOZAEUKZXFE
      RZXMUAZXEERZXEXLIUGZUAZUAXFXEOZULZDUMAUMZXLXARZXNYEXQYDADXRXTXMYAYCXRXTUA
      ZXMYAUAZYCYGYHXPXEIUJZXLOZUAZYCXRXPXMXTYJYAWSXRXPXMUNXKEXFXLIBUOZUPUQWSXT
      YJYAUNXKEXEXLIYLUPUQURYGXOYIOZYCYKYGYMXGXCXESTZOZCEUSYCYGYMYOCEYGXDYMYOYG
      XDYMYOYGXDUAZYMUAXCXOUJZXCYIUJZXGYNYMYQYROYPXCXOYIUTVAYPYQXGOZYMXRXDYSXTX
      FXCVBVCVDYPYRYNOZYMXTXDYTXRXEXCVBVEVDVFVGVHVICXFXEVJVKXOYIXLVLVMVNVOVPVQV
      RWSYFXQUNXKAEXLIVSVTXNXSAUHYEXMXSAXMXRXMXFIWAEXFXLIAUOWBABEXHIXIXJWCWDWEW
      FXSYBADYCXRXTXMYAXFXEEWGXFXEXLIWHWIWJWKWLWMWNWOEWRIWPWQ $.
      $( [7-Jul-2006] $) $( [26-May-2006] $)

    $( A bra is a continuous linear functional. $)
    bracnlnt $p |- ( A e. H~ -> ( bra ` A ) e. ( LinFn i^i ConFn ) ) $=
      ( chil clf ccnf cin cbr wf1o wf bra11 f1of ax-mp ffvelrni ) BCDEZAFBMFGBM
      FHIBMFJKL $.
      $( [8-Jul-2006] $) $( [30-May-2006] $)

    $( Value of the converse of the bra function.  Based on the Riesz Lemma
       ~ riesz4t , this very important theorem not only justifies the Dirac
       bra-ket notation, but allows us to extract a unique vector from any
       continuous linear functional from which the functional can be recovered;
       i.e. a single vector can "store" _all_ of the information contained in
       any entire continuous linear functional (mapping from ` H~ ` to
       ` CC ` ). $)
    cnvbravalt $p |- ( T e. ( LinFn i^i ConFn ) ->
      ( `' bra ` T ) = U. { y e. H~ | A. x e. H~ ( T ` x ) = ( x .ih y ) } ) $=
      ( clf ccnf cin wcel cv cfv csp co wceq chil wral crab cuni cbr ccnv wa
      wrex crn rnbra eleq2i biimpr wfn wb wf wf1o bra11 f1of ax-mp ffn fvelrnb
      sylib adantr wi f1ocnvfv mpan imp opreq2d adantll bravalvalt ancoms
      fveq1 adantl 3eqtr2rd exp31 r19.23adv mpd r19.21aiva wreu opreq2 eqeq2d
      ralbidv reuuni2 f1ocnvdm riesz4t sylanc mpbid eqcomd ) CDEFZGZAHZCIZWCBHZ
      JKZLZAMNZBMOPZCQRIZWBWDWCWJJKZLZAMNZWIWJLZWBWLAMWBWCMGZSZWEQIZCLZBMTZWLWB
      WSWOWBCQUAZGZWSXAWBWTWACUBUCUDQMUEZXAWSUFMWAQUGZXBMWAQUHZXCUIMWAQUJUKMWAQ
      ULUKBMCQUMUKUNUOWPWRWLBMWPWEMGZWRWLWPXESZWRSWKWFWCWQIZWDXEWRWKWFLWPXEWRSW
      JWEWCJXEWRWJWELZXDXEWRXHUPUIMWAWECQUQURUSUTVAXFXGWFLZWRWOXEXIWBXEWOXIWEWC
      VBVCVAUOWRXGWDLXFWCWQCVDVEVFVGVHVIVJWJMGZWHBMVKWMWNUFWBWHWMBMWJWEWJLZWGWL
      AMXKWFWKWDWEWJWCJVLVMVNVOXDWBXJUIMWACQVPURBACVQVRVSVT $.
      $( [9-Jul-2006] $) $( [26-May-2006] $)

    $( Closure of the converse of the bra function. $)
    cnvbraclt $p |- ( T e. ( LinFn i^i ConFn ) -> ( `' bra ` T ) e. H~ ) $=
      ( chil clf ccnf cin cbr wf1o wcel ccnv cfv bra11 f1ocnvdm mpan ) BCDEZFGA
      NHAFIJBHKBNAFLM $.
      $( [10-Jul-2006] $) $( [26-May-2006] $)

    $( The converse bra of the bra of a vector is the vector itself. $)
    cnvbrabrat $p |- ( A e. H~ -> ( `' bra ` ( bra ` A ) ) = A ) $=
      ( chil clf ccnf cin cbr wf1o wcel cfv ccnv wceq bra11 f1ocnvfv1 mpan ) BC
      DEZFGABHAFIFJIAKLBOAFMN $.
      $( [11-Jul-2006] $) $( [30-May-2006] $)

    $( The bra of the converse bra of a continuous linear functional. $)
    bracnvbrat $p |- ( T e. ( LinFn i^i ConFn ) ->
                     ( bra ` ( `' bra ` T ) ) = T ) $=
      ( chil clf ccnf cin cbr wf1o wcel ccnv cfv wceq bra11 f1ocnvfv2 mpan ) BC
      DEZFGAOHAFIJFJAKLBOAFMN $.
      $( [10-Jul-2006] $) $( [31-May-2006] $)

    $( The vector that a continuous linear functional is the bra of. $)
    bracnlnvalt $p |- ( T e. ( LinFn i^i ConFn ) ->
         T = ( bra ` U. { y e. H~ | A. x e. H~ ( T ` x ) = ( x .ih y ) } ) ) $=
      ( clf ccnf cin wcel cv cfv csp co wceq chil wral crab cuni cbr ccnv
      cnvbravalt wb cnvbraclt eqeltrrd wf1o bra11 f1ocnvfvb mp3an1 mpancom
      mpbird eqcomd ) CDEFZGZAHZCIULBHJKLAMNBMOPZQIZCUKUNCLZCQRIZUMLZABCSZUMMGZ
      UKUOUQTZUKUPUMMURCUAUBMUJQUCUSUKUTUDMUJUMCQUEUFUGUHUI $.
      $( [?] $) $( [26-May-2006] $)

    $( Multiplication property of the converse bra function. $)
    cnvbramult $p |- ( ( A e. CC /\ T e. ( LinFn i^i ConFn ) ) ->
       ( `' bra ` ( A .fn T ) ) = ( ( * ` A ) .h ( `' bra ` T ) ) ) $=
      ( cc wcel clf ccnf cin wa ccj cfv cbr ccnv csm co chft chil wceq
      brafnmult cjclt sylan cjcjt adantr opreq1d eqtrd cnvbraclt sylan2
      bracnvbrat opreq2d adantl fveq2d hvmulclt syl2an cnvbrabrat syl eqtr3d )
      ACDZBEFGDZHZAIJZBKLZJZMNZKJZUTJZABONZUTJVBURVCVEUTURVCAVAKJZONZVEUPVAPDZV
      CVGQUQUPVHHZVCUSIJZVFONZVGUSCDZVHVCVKQUPUSVARASZTVIVJAVFOUPVJAQVHAUAUBUCU
      DBUEZUFUQVGVEQUPUQVFBAOBUGUHUIUDUJURVBPDZVDVBQVLVHVOUPUQUSVAUKVMVNULVBUMU
      NUO $.
      $( [13-Jul-2006] $) $( [31-May-2006] $)

    $( Dirac bra-ket associative law ` ( | A >. ` ` <. B | ) | C >. = `
       ` | A >. ( <. B | C >. ) ` i.e. the juxtaposition of an outer product
       with a ket equals a bra juxtaposed with an inner product.  Since
       ` <. B | C >. ` is a complex number, it is the first argument in the
       inner product ` .h ` that it is mapped to, although in Dirac notation
       it is placed after the ket. $)
    kbass1t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                 ( ( A ketbra B ) ` C ) = ( ( ( bra ` B ) ` C ) .h A ) ) $=
      ( chil wcel w3a ck co cfv csp csm cbr kbvalvalt wceq bravalvalt 3adant1
      opreq1d eqtr4d ) ADEZBDEZCDEZFZCABGHICBJHZAKHCBLIIZAKHABCMUBUDUCAKTUAUDUC
      NSBCOPQR $.
      $( [18-May-2006] $) $( [15-May-2006] $)

    $( Dirac bra-ket associative law ` ( <. A | B >. ) <. C | = `
       ` <. A | ( | B >. ` ` <. C | ) ` i.e. the juxtaposition of an inner
       product with a bra equals a ket juxtaposed with an outer product. $)
    kbass2t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                  ( ( ( bra ` A ) ` B ) .fn ( bra ` C ) ) =
                  ( ( bra ` A ) o. ( B ketbra C ) ) ) $=
      ( vx vy chil wcel w3a cbr cfv chft co cv cmul wceq wa copab csp csm csb
      ck ccom cc wf hfmmvalt braclt brafnt syl2an 3impa axmulcom hiclt ancoms
      3adant3 adantr 3ad2antl3 sylanc ax-his3 3simp2 3simp1 syl3anc eqtr4d
      bravalvalt opreq12d cvv oprex csbopr1g ax-mp csbvarg opreq1i eqtr a1i
      3eqtr4d eqeq2d pm5.32da opabbidv bravalt coeq1d kbvalt coeq2d sylan9eq
      3impb crn wss wral hvmulclt sylan ancom31s r19.21aiva eqid rnssopab
      sylib fopabcos syl 3adant1 eqtr2d 3eqtrd ) AFGZBFGZCFGZHZBAIJZJZCIJZKLZDM
      ZFGZEMZXBXEXCJZNLZOZPZDEQZXFXGDXECRLZBSLZXEARLZTZOZPZDEQZXABCUALZUBZWQWRW
      SXDXLOZXBUCGFUCXCUDYBWQWRPWSDEXBXCUEABUFCUGUHUIWTXKXRDEWTXFXJXQWTXFPZXIXP
      XGYCBARLZXMNLZXNARLZXIXPYCYEXMYDNLZYFYDUCGZXMUCGZYEYGOYCYDXMUJWTYHXFWQWRY
      HWSWRWQYHBAUKULUMUNWSWQXFYIWRXFWSYIXECUKZULUOZUPYIWRWQYFYGOYCXMBAUQYKWTWR
      XFWQWRWSURUNWTWQXFWQWRWSUSUNUTVAYCXBYDXHXMNWTXBYDOZXFWQWRYLWSABVBUMUNWSWQ
      XFXHXMOWRCXEVBUOVCXPYFOYCXPDXNXETZARLZYFXNVDGZXPYNOXMBSVEZDXNXEAVDRVFVGYM
      XNARYOYMXNOYPDXNVDVHVGVIVJVKVLVMVNVOWTYAXFXGXOOPDEQZXFXGXNOPDEQZUBZXSWQWR
      WSYAYSOWQWRWSPZYAYQXTUBYSWQXAYQXTDEAVPVQYTXTYRYQDEBCVRVSVTWAWRWSYSXSOZWQY
      TYRWBFWCZUUAYTXNFGZDFWDUUBYTUUCDFXFWSWRUUCYIWRUUCXFWSPXMBWEYJWFWGWHDEFFXN
      YRYRWIZYPWJWKDEFFXOXNYQYRXEARVEYPYQWIUUDWLWMWNWOWP $.
      $( [5-Jun-2006] $) $( [23-May-2006] $)

    $( Dirac bra-ket associative law ` <. A | B >. ` ` <. C | D >. = `
       ` ( <. A | B >. ` ` <. C | ) | D >. ` . $)
    kbass3t $p |- ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. H~ /\ D e. H~ ) ) ->
                  ( ( ( bra ` A ) ` B ) x. ( ( bra ` C ) ` D ) ) =
         ( ( ( ( bra ` A ) ` B ) .fn ( bra ` C ) ) ` D ) ) $=
      ( chil wcel wa cbr cfv chft co cmul cc wf wceq hfmvalt braclt adantr
      brafnt ad2antrl simprr syl3anc eqcomd ) AEFBEFGZCEFZDEFZGZGZDBAHIIZCHIZJK
      IZUIDUJILKZUIMFZEMUJNZUFUKULOUHUIDUJPUDUMUGABQRUEUNUDUFCSTUDUEUFUAUBUC $.
      $( [22-Oct-2006] $) $( [30-May-2006] $)

    $( Dirac bra-ket associative law ` <. A | B >. ` ` <. C | D >. = `
       ` <. A | ( | B >. ` ` <. C | D >. ) ` . $)
    kbass4t $p |- ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. H~ /\ D e. H~ ) ) ->
                  ( ( ( bra ` A ) ` B ) x. ( ( bra ` C ) ` D ) ) =
            ( ( bra ` A ) ` ( ( ( bra ` C ) ` D ) .h B ) ) ) $=
      ( chil wcel wa cbr cfv cmul co csm cc wceq axmulcom braclt syl2an clf
      lnfnmult bralnfnt ad2antrr adantl simplr syl3anc eqtr4d ) AEFZBEFZGZCEFDE
      FGZGZBAHIZIZDCHIIZJKZUMULJKZUMBLKUKIZULMFUMMFZUNUONUHUIULUMOABPCDPZQUKRFZ
      UQUGUPUONUJUMBUKSUFUSUGUIATUAUIUQUHURUBUFUGUIUCUDUE $.
      $( [22-Oct-2006] $) $( [30-May-2006] $)

    $( Dirac bra-ket associative law ` ( | A >. ` ` <. B | ) ( | C >. `
       ` <. D | ) = ( ( | A >. ` ` <. B | ) | C >. ) <. D | ` . $)
    kbass5t $p |- ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. H~ /\ D e. H~ ) ) ->
                  ( ( A ketbra B ) o. ( C ketbra D ) ) =
                  ( ( ( A ketbra B ) ` C ) ketbra D ) ) $=
      ( vx vy chil wcel wa cv csp co csm wceq copab ccom ck cfv csb crn wss
      kbvalt rneqd wf kbopt frn syl eqsstr3d adantl oprex eqid fopabcos cmul
      cc ax-his3 hiclt ancoms adantll pm3.26 ad2antlr simpll syl3anc adantlll
      opreq1d ax-hvmulass ad2ant2lr adantr ad2antrr eqtrd cvv csbopr1g ax-mp
      csbvarg opreq1i eqtr syl5eq eqeq2d pm5.32da opabbidv coeq1d coeq2d
      kbvalvalt 3expa hvmulclt sylan ancom31s anasss 3eqtr4d ) AGHZBGHZIZCGHZDG
      HZIZIZEJZGHZFJZWPBKLZAMLZNIEFOZWQWRWPDKLZCMLZNIEFOZPZWQWRXBCBKLZAMLZMLZNZ
      IZEFOZABQLZCDQLZPZCXLRZDQLZWOXEWQWREXCWTSZNZIZEFOZXKWOXDTZGUAZXEXTNWNYBWK
      WNYAXMTZGWNXMXDEFCDUBZUCWNGGXMUDYCGUACDUEGGXMUFUGUHUIEFGGWTXCXAXDWSAMUJXB
      CMUJZXAUKXDUKULUGWOXSXJEFWOWQXRXIWOWQIZXQXHWRYFXCBKLZAMLZXHXQYFYHXBXFUMLZ
      AMLZXHYFYGYIAMWJWNWQYGYINZWIXBUNHZWLWJYKWJWNIWQIXBCBUOWNWQYLWJWMWQYLWLWQW
      MYLWPDUPUQURZURWNWLWJWQWLWMUSUTWJWNWQVAVBVCVDYLXFUNHZWIYJXHNYFXBXFAVEWNWQ
      YLWKYMURWOYNWQWJWLYNWIWMWLWJYNCBUPZUQVFVGWKWIWNWQWIWJUSVHVBVIXQEXCWSSZAML
      ZYHXCVJHZXQYQNYEEXCWSAVJMVKVLYPYGAMYPEXCWPSZBKLZYGYRYPYTNYEEXCWPBVJKVKVLY
      SXCBKYRYSXCNYEEXCVJVMVLVNVOVNVOVPVQVRVSVIWOXNXAXMPXEWOXLXAXMWKXLXANWNEFAB
      UBVGVTWOXMXDXAWNXMXDNWKYDUIWAVIWKWLWMXPXKNWKWLIZWMIZXPXGDQLZXKUUBXOXGDQUU
      AXOXGNZWMWIWJWLUUDABCWBWCVGVDXGGHZWMUUCXKNUUAEFXGDUBWLWJWIUUEYNWIUUEWLWJI
      XFAWDYOWEWFWEVIWGWH $.
      $( [30-Jun-2006] $) $( [30-May-2006] $)

    $( Dirac bra-ket associative law ` ( | A >. ` ` <. B | ) ( | C >. `
       ` <. D | ) = | A >. ` ` ( <. B | ( | C >. ` ` <. D | ) ) ` . $)
    kbass6t $p |- ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. H~ /\ D e. H~ ) ) ->
                  ( ( A ketbra B ) o. ( C ketbra D ) ) =
               ( A ketbra ( `' bra ` ( ( bra ` B ) o. ( C ketbra D ) ) ) ) ) $=
      ( chil wcel wa ck co ccom cfv csp csm cbr ccnv kbass5t wceq kbvalvalt
      3expa adantrr opreq1d ccj wi cc kbmult hiclt syl3an1 3exp ex com13 imp43
      chft clf ccnf cin cnvbramult braclt bracnlnt syl2an bravalvalt fveq2d
      cnvbrabrat opreqan12d eqtr2d anasss kbass2t 3expb adantll opreq2d eqtr4d
      3eqtrd ) AEFZBEFZGZCEFZDEFZGZGZABHIZCDHIZJCVSKZDHICBLIZAMIZDHIZABNKZVTJZN
      OZKZHIZABCDPVRWAWCDHVNVOWAWCQZVPVLVMVOWJABCRSTUAVRWDAWBUBKZDMIZHIZWIVLVMV
      OVPWDWMQZVOVMVLVPWNUCZVOVMVLWOUCVOVMGZVLVPWNWBUDFVLVPWNWPWBADUECBUFUGUHUI
      UJUKVRWHWLAHVMVQWHWLQVLVMVQGZWLCWEKZDNKZULIZWGKZWHVMVOVPWLXAQVMVOGZVPGXAW
      RUBKZWSWGKZMIZWLWRUDFWSUMUNUOFXAXEQXBVPWRWSUPBCUQDURUSXBVPXCWKXDDMXBWRWBU
      BBCUTVADVBVCVDVEWQWTWFWGVMVOVPWTWFQBCDVFVGVAVDVHVIVJVK $.
      $( [22-Oct-2006] $) $( [30-May-2006] $)

