$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                  Preliminary ZFC lemmas
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( To convert to pure ZFC, put the definitions of ` +h ` , ` .h ` ,
     and ` .ih ` here. $)

  ${
    $d x y $.
    $( Define the function for the norm of a vector of Hilbert space.  See
       ~ normval for its value and ~ normcl for its closure.  Theorems
       ~ norm-i.i , ~ norm-ii.i , and ~ norm-iii.i show it has the expected
       properties of a norm.  In the literature, the norm of ` A ` is usually
       written "|| ` A ` ||", but we use function notation to take advantage of
       our existing theorems about functions.  Definition of norm in [Beran]
       p. 96. $)
    df-hnorm $a |- normh =
         { <. x , y >. | ( x e. dom dom .ih /\ y = ( sqr ` ( x .ih x ) ) ) } $.
  $}

  $( Define base set of Hilbert space, for use if we want to develop Hilbert
     space independently from the axioms (see comments in ~ ax-hilex ).  Note
     that ` ~H ` is considered a primitive in the Hilbert space axioms below,
     and we don't use this definition outside of this section.  This definition
     can be proved independently from those axioms as as theorem ~ hhba . $)
  df-hba $a |- ~H = ( BaseSet ` <. <. +h , .h >. , normh >. ) $.

  $( Define the zero vector of Hilbert space.  Note that ` 0vec ` is considered
     a primitive in the Hilbert space axioms below, and we don't use this
     definition outside of this section.  It is proved from the axioms as
     theorem ~ hh0v . $)
  df-h0v $a |- 0h = ( 0vec ` <. <. +h , .h >. , normh >. ) $.

  ${
    $d x y z $.
    $( Define vector subtraction.  See ~ hvsubvali for its value and ~ hvsubcli
       for its closure. $)
    df-hvsub $a |- -h = { <. <. x , y >. , z >. | ( ( x e. ~H /\ y e. ~H )
                   /\ z = ( x +h ( -u 1 .h y ) ) ) } $.
  $}

  ${
    $d x y z f w $.
    $( Define the limit relation for Hilbert space.  See ~ hlimi for its
       relational expression.  Note that ` f : NN --> ~H ` is an infinite
       sequence of vectors, i.e. a mapping from integers to vectors.
       Definition of converge in [Beran] p. 96. $)
    df-hlim $a |- ~~>v = { <. f , w >. | ( ( f : NN --> ~H /\ w e. ~H ) /\
          A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( normh ` ( ( f ` z ) -h w ) ) < x ) ) ) } $.
  $}

  ${
    $d x y z w f $.
    $( Define the set of Cauchy sequences on a Hilbert space.  See ~ hcau for
       its membership relation.  Note that ` f : NN --> ~H ` is an infinite
       sequence of vectors, i.e. a mapping from integers to vectors.
       Definition of Cauchy sequence in [Beran] p. 96. $)
    df-hcau $a |- Cauchy = { f | ( f : NN --> ~H /\
          A. x e. RR ( 0 < x ->
          E. y e. NN A. z e. NN A. w e. NN ( ( y <_ z /\ y <_ w ) ->
             ( normh ` ( ( f ` z ) -h ( f ` w ) ) ) < x ) ) ) } $.
  $}

  ${
    $d x y z U $.
    h2h.1 $e |- U = <. <. +h , .h >. , normh >. $.
    h2h.2 $e |- U e. NrmCVec $.
    $( The group (addition) operation of Hilbert space. $)
    h2hva $p |- +h = ( +v ` U ) $=
      ( cva csm cop cno cpv cfv c1st eqid vafval opex op1st fveq2i cvv wcel
      cnv w3a eqeltrri nvex ax-mp simp1i 3eqtrri eqtr4i ) DDEFZGFZHIZAHIUHUGJIZ
      JIUFJIDUGUHUHKLUIUFJUFGDEMNODEDPQZEPQZGPQZUGRQUJUKULSAUGRBCTEDGUAUBUCNUDA
      UGHBOUE $.
      $( [31-May-2008] $)

    $( The scalar product operation of Hilbert space. $)
    h2hsm $p |- .h = ( .s ` U ) $=
      ( csm cva cop cno cns cfv c1st c2nd eqid smfval opex op1st fveq2i cvv
      wcel cvc wa cnv eqeltrri nvvcop ax-mp vcex simpli simpri op2nd 3eqtrri
      eqtr4i ) DEDFZGFZHIZAHIUMULJIZKIUKKIDUMULUMLMUNUKKUKGEDNOPEDEQRZDQRZUKSRZ
      UOUPTULUARUQAULUABCUBDEGUCUDDEUEUDZUFUOUPURUGUHUIAULHBPUJ $.
      $( [31-May-2008] $)

    $( The norm function of Hilbert space. $)
    h2hnm $p |- normh = ( norm ` U ) $=
      ( cnm cfv cva csm cop cno c2nd fveq2i eqid nmfval opex cvv wcel cnv w3a
      eqeltrri nvex ax-mp simp3i op2nd 3eqtrri ) ADEFGHZIHZDEZUFJEIAUFDBKUFUGUG
      LMUEIFGNFOPZGOPZIOPZUFQPUHUIUJRAUFQBCSGFITUAUBUCUD $.
      $( [5-Jun-2008] $)

    ${
      h2h.4 $e |- ~H = ( BaseSet ` U ) $.
      $( The vector subtraction operation of Hilbert space. $)
      h2hvs $p |- -h = ( -v ` U ) $=
        ( vx vy vz cmv cv chil wcel wa c1 cneg csm co cva wceq copab2 cnsb cfv
        df-hvsub cnv h2hva h2hsm eqid nvmfval ax-mp eqtr4i ) HEIZJKFIZJKLGIUJMN
        UKOPQPRLEFGSZATUAZEFGUBAUCKUMULRCEFGOAQUMJDABCUDABCUEUMUFUGUHUI $.
        $( [6-Jun-2008] $)
    $}

    ${
      h2hm.4 $e |- ~H = ( BaseSet ` U ) $.
      h2hm.5 $e |- D = ( IndMet ` U ) $.
      $( The base set for the metric for Hilbert space. $)
      h2hmetba $p |- ~H = dom dom D $=
        ( chil imsbai ) ABGEFDH $.
        $( [6-Jun-2008] $)

      $( Value of the distance function of the metric space of Hilbert
         space. $)
      h2hmetdval $p |- ( ( A e. ~H /\ B e. ~H )
                  -> ( A D B ) = ( normh ` ( A -h B ) ) ) $=
        ( cnv wcel chil co cmv cno cfv wceq h2hvs h2hnm imsdval mp3an1 ) DIJAKJ
        BKJABCLABMLNOPFABCDMNKGDEFGQDEFRHST $.
        $( [6-Jun-2008] $)
    $}
  $}

  ${
    $d x y z U $.  $d f w x y z D $.
    h2hc.1 $e |- U = <. <. +h , .h >. , normh >. $.
    h2hc.2 $e |- U e. NrmCVec $.
    h2hc.4 $e |- ~H = ( BaseSet ` U ) $.
    h2hc.3 $e |- D = ( IndMet ` U ) $.
    $( The Cauchy sequences of Hilbert space. $)
    h2hcau $p |- Cauchy = ( ( Cau ` D ) i^i ( ~H ^m NN ) ) $=
      ( vf vx vy vz vw cn chil cv wf cc0 clt wbr cle wa cfv cmv co cno wi wral
      wrex cr cab cc cxp wss wcel w3a cz cin ccau cca cmap wb ffvelrn adantr
      adantlr h2hmetdval breq1d ibar bitr3d df-3an syl6bbr syl11anc imbi2d
      ralbidva rexbidv c1 1z nnuz cau5i syl6rbbr ralbidv fssxp nnsscn ssid
      xpss12 mp2an sstr mpan2 syl biantrurd pm5.32i ancom bitri abbii inab
      eqtr4i df-hcau cme wceq cnv imsmet ax-mp imsbai caufval hlex nnex mapval
      ineq12i 3eqtr4i ) LMGNZOZPHNZQRZINZJNZSRXLKNZSRTZXMXHUAZXNXHUAZUBUCUDUAZX
      JQRZUEZKLUFZJLUFZILUGZUEZHUHUFZTZGUIZXHUJMUKZULZXKXOXPMUMZXQMUMZXPXQAUCZX
      JQRZUNZUEZKUOUFJUOUFIUOUGZUEZHUHUFZTZGUIZXIGUIZUPZUQAURUAZMLUSUCZUPYGYSXI
      TZGUIUUBYFUUEGYFXIYSTUUEXIYEYSXIYRYEYSXIYQYDHUHXIYPYCXKXIYCYOKLUFZJLUFZIL
      UGYPXIYBUUGILXIYAUUFJLXIXMLUMZTZXTYOKLUUIXNLUMZTZXSYNXOUUKYJYKXSYNUTUUIYJ
      UUJLMXMXHVAVBXIUUJYKUUHLMXNXHVAVCYJYKTZXSUULYMTZYNUULYMXSUUMUULYLXRXJQXPX
      QABCDEFVDVEUULYMVFVGYJYKYMVHVIVJVKVLVLVMYNJKIVNLVOVPVQVRVKVSXIYIYRXIXHLMU
      KZULZYILMXHVTUUOUUNYHULZYILUJULMMULUUPWAMWBLUJMMWCWDXHUUNYHWEWFWGWHVGWIXI
      YSWJWKWLYSXIGWMWNHIJKGWOUUCYTUUDUUAAWPUMZUUCYTWQBWRUMUUQDABFWSWTHAGIJKMAB
      MEFDXAXBWTMLGBMEXCXDXEXFXG $.
      $( [6-Jun-2008] $)

  $}

  ${
    $d x y z U $.  $d f w x y z D $.
    h2hl.1 $e |- U = <. <. +h , .h >. , normh >. $.
    h2hl.2 $e |- U e. NrmCVec $.
    h2hl.4 $e |- ~H = ( BaseSet ` U ) $.
    h2hl.3 $e |- D = ( IndMet ` U ) $.
    $( The limit sequences of Hilbert space. $)
    h2hlm $p |- ~~>v = ( ( ~~>m ` D ) |` ( ~H ^m NN ) ) $=
      ( vf vw vx vy vz cn chil cv wf wcel wa cc0 clt wbr cle cfv cmv co cno wi
      wral wrex cr copab cc cxp wss cz w3a cmap cres chli clm wb h2hmetdval
      breq1d ibar adantr bitr3d ffvelrn sylan an32s imbi2d ralbidva rexbidv c1
      1z cuz nnuz eqimss2i nnssz cvg3i syl6bbr ralbidv pm5.32da fssxp nnsscn
      ssid xpss12 mp2an sstr mpan2 syl biantrurd bitrd 3anass pm5.32i anass
      hlex nnex elmap anbi1i 3bitr4i opabbii resopab eqtr4i df-hlim wceq cme
      cnv imsmet ax-mp imsbai lmfval reseq1 3eqtr4i ) LMGNZOZHNZMPZQZRINZSTZJNK
      NZUATZXTXMUBZXOUCUDUEUBZXRSTZUFZKLUGZJLUHZUFZIUIUGZQZGHUJZXMUKMULZUMZXPXS
      YAYBMPZYBXOAUDZXRSTZQZUFZKUNUGJUNUHZUFZIUIUGZUOZGHUJZMLUPUDZUQZURAUSUBZUU
      DUQZYKXMUUDPZUUBQZGHUJUUEYJUUIGHXNXPYIQZQXNUUBQYJUUIXNUUJUUBXNUUJYMXPUUAQ
      ZQZUUBXNUUJUUKUULXNXPYIUUAXQYHYTIUIXQYGYSXSXQYGYRKLUGZJLUHYSXQYFUUMJLXQYE
      YRKLXQXTLPZQYDYQYAXNUUNXPYDYQUTZYNXPUUOXNUUNQYNXPQZYPYDYQUUPYOYCXRSYBXOAB
      CDEFVAVBYNYPYQUTXPYNYPVCVDVELMXTXMVFVGVHVIVJVKYQJKVLVLLLVMLVLVNUBVOVPZVQV
      MUUQVQVRVSVIVTWAXNYMUUKXNXMLMULZUMZYMLMXMWBUUSUURYLUMZYMLUKUMMMUMUUTWCMWD
      LUKMMWEWFXMUURYLWGWHWIWJWKYMXPUUAWLVSWMXNXPYIWNUUHXNUUBMLXMBMEWOWPWQWRWSW
      TUUBGHUUDXAXBIJKHGXCUUFUUCXDZUUGUUEXDAXEPZUVABXFPUVBDABFXGXHIHAGJKMABMEFD
      XIXJXHUUFUUCUUDXKXHXL $.
      $( [6-Jun-2008] $)
  $}

