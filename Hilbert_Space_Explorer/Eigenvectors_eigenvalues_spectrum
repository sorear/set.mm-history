$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Eigenvectors, eigenvalues, spectrum
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

    $( Define the eigenvector function.  Theorem ~ eleigveccl shows that
       ` eigvec `` T ` , the set of eigenvectors of Hilbert space operator
       ` T ` , are Hilbert space vectors. $)
    df-eigvec $a |- eigvec = ( t e. ( ~H ^m ~H ) |->
         { x e. ( ~H \ 0H ) | E. z e. CC ( t ` x ) = ( z .h x ) } ) $.

    $( Define the eigenvalue function.  The range of ` eigval `` T ` is the set
       of eigenvalues of Hilbert space operator ` T ` .  Theorem ~ eigvalcl
       shows that ` ( eigval `` T ) `` A ` , the eigenvalue associated with
       eigenvector ` A ` , is a complex number. $)
    df-eigval $a |- eigval = ( t e. ( ~H ^m ~H ) |->
                     ( x e. ( eigvec ` t ) |->
                   ( ( ( t ` x ) .ih x ) / ( ( normh ` x ) ^ 2 ) ) ) ) $.

    $( Define the spectrum of an operator.  Definition of spectrum in [Halmos]
       p. 50. $)
    df-spec $a |- Lambda = ( t e. ( ~H ^m ~H ) |->
        { x e. CC | -. ( t -op ( x .op ( _I |` ~H ) ) ) : ~H -1-1-> ~H } ) $.
  $}

