$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Orthocomplements
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Define orthogonal complement of a subset (usually a subspace) of
       Hilbert space.  The orthogonal complement is the set of all vectors
       orthogonal to all vectors in the subset.  See ~ ocvalt and ~ chocval
       for its value.  Textbooks usually denote this unary operation with the
       symbol ` _|_ ` as a small superscript, although Mittelstaedt uses
       the symbol as a prefix operation.  Here we define a function (prefix
       operation) ` _|_ ` rather than introducing a new syntactical form.
       This lets us take advantage of the theorems about functions that we
       already have proved under set theory.  Definition of [Mittelstaedt]
       p. 9. $)
    df-oc $a |- _|_ = { <. x , y >. | ( x (_ H~ /\ y =
                         { z e. H~ | A. w e. x ( z .ih w ) = 0 } ) } $.
  $}

  $( Define the zero for closed subspaces of Hilbert space.  See ~ h0elch
     for closure law. $)
  df-ch0 $a |- 0H = { 0h } $.

  $( Membership in zero for closed subspaces of Hilbert space. $)
  elch0 $p |- ( A e. 0H <-> A = 0h ) $=
    ( c0h wcel c0v csn wceq df-ch0 eleq2i chil ax-hv0cl elisseti elsnc2 bitr )
    ABCADEZCADFBNAGHADDIJKLM $.
    $( [6-Apr-2001] $)

  $( The zero subspace is a closed subspace.  Part of Proposition 1 of
     [Kalmbach] p. 65. $)
  h0elch $p |- 0H e. CH $=
    ( c0h c0v csn cch df-ch0 hsn0elch eqeltr ) ABCDEFG $.
    $( [30-May-1999] $)

  $( The zero subspace is a subspace of Hilbert space. $)
  h0elsh $p |- 0H e. SH $=
    ( c0h h0elch chshi ) ABC $.
    $( [3-Jun-2004] $) $( [2-Jun-2004] $)

  ${
    $d x y z w H $.  $d x y z w A $.  $d x y B $.
    $( Value of orthogonal complement of a subset of Hilbert space. $)
    ocvalt $p |- ( H (_ H~ -> ( _|_ ` H ) =
        { x e. H~ | A. y e. H ( x .ih y ) = 0 } ) $=
      ( vz vw chil wss cpw wcel cort cfv cv csp co cc0 wceq wral crab ax-hilex
      elpw2 raleq1 rabbisdv wa copab df-oc anbi1i opabbii eqtr4 rabex fvopab4
      sylbir ) CFGCFHZICJKALBLMNOPZBCQZAFRZPCFSTDECUMBDLZQZAFRZUOULJUPCPUQUNAFU
      MBUPCUAUBJUPFGZELURPZUCZDEUDUPULIZUTUCZDEUDDEABUEVCVADEVBUSUTUPFSTUFUGUHU
      NAFSUIUJUK $.
      $( [7-Aug-2000] $)

    $( Membership in orthogonal complement of H subset. $)
    ocelt $p |- ( H (_ H~ -> ( A e. ( _|_ ` H ) <-> ( A e. H~ /\
                  A. x e. H ( A .ih x ) = 0 ) ) ) $=
      ( vy chil wss cort cfv wcel cv csp co cc0 wceq wral crab wa ocvalt
      eleq2d opreq1 eqeq1d ralbidv elrab syl6bb ) CEFZBCGHZIBDJZAJZKLZMNZACOZDE
      PZIBEIBUHKLZMNZACOZQUEUFULBDACRSUKUODBEUGBNZUJUNACUPUIUMMUGBUHKTUAUBUCUD
      $.
      $( [7-Aug-2000] $)

    $( Membership in orthogonal complement of H subspace. $)
    shocelt $p |- ( H e. SH -> ( A e. ( _|_ ` H ) <-> ( A e. H~ /\
                  A. x e. H ( A .ih x ) = 0 ) ) ) $=
      ( csh wcel chil wss cort cfv cv csp co cc0 wceq wral wa wb shss ocelt
      syl ) CDECFGBCHIEBFEBAJKLMNACOPQCRABCST $.
      $( [9-Oct-1999] $)

    $( The orthogonal complement of a subspace is a subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    ocsh $p |- ( A (_ H~ -> ( _|_ ` A ) e. SH ) $=
      ( vx vy vz chil wss cort cfv c0v wcel wa cv cva co wral csm cc csh csp
      cc0 wceq crab ssrab2 ocvalt sseq1d mpbiri ssel hi01t syl6 r19.21aiv
      ax-hv0cl jctil ocelt mpbird jca wi caddc ax-his2 3expa opreq12 0cn
      addid1 syl6eq sylan9eq ex ancoms ssel2 sylan an1rs r19.20dva imdistand
      hvaddclt anim1i anbi12d an4 r19.26 anbi2i bitr4 syl6bb 3imtr4d exp3a
      r19.21adv cmul opreq2 eqeq1d mul01t syl5cbir ad2antrl wb w3a ax-his3
      sylibrd hvmulclt anbi2d anass syl6bbr sh sylibr ) AEFZAGHZEFZIWTJZKZBLZCL
      ZMNZWTJZCWTOZBWTOZXDXEPNZWTJZCWTOZBQOZKZKWTRJWSXCXNWSXAXBWSXAXDXESNTUACAO
      ZBEUBZEFXOBEUCWSWTXPEBCAUDUEUFWSXBIEJZIXESNTUAZCAOZKWSXSXQWSXRCAWSXEAJXEE
      JZXRAEXEUGXEUHUIUJUKULCIAUMUNUOWSXIXMWSXHBWTWSXDWTJZXGCWTWSYAXEWTJZXGWSXD
      EJZXTKZXDDLZSNZTUAZXEYESNZTUAZKZDAOZKZXFEJZXFYESNZTUAZDAOZKZYAYBKZXGWSYLY
      DYPKYQWSYDYKYPWSYDYKYPUPWSYDKYJYODAWSYEAJZYDYJYOUPZYEEJZYDYTWSYSKZYDUUAYT
      YDUUAKZYJYOUUCYJYNYFYHUQNZTYCXTUUAYNUUDUAXDXEYEURUSYJUUDTTUQNTYFTYHTUQUTT
      VAVBVCVDVEVFAEYEVGZVHVIVJVEVKYDYMYPXDXEVLVMUIWSYRYCYGDAOZKZXTYIDAOZKZKZYL
      WSYAUUGYBUUIDXDAUMDXEAUMZVNUUJYDUUFUUHKZKYLYCUUFXTUUHVOYKUULYDYGYIDAVPVQV
      RVSDXFAUMVTWAWBUJWSXLBQWSXDQJZXKCWTWSUUMYBXKWSUUMXTKZUUHKZXJEJZXJYESNZTUA
      ZDAOZKZUUMYBKZXKWSUUOUUNUUSKUUTWSUUNUUHUUSWSUUNUUHUUSUPWSUUNKYIUURDAWSYSU
      UNYIUURUPZUUAUUNUVBUUBUUAUUNKYIXDYHWCNZTUAZUURUUMYIUVDUPUUAXTYIUVDXDTWCNZ
      TUAUUMYIUVCUVETYHTXDWCWDWEXDWFWGWHUUNUUAUURUVDWIZUUMXTUUAUVFUUMXTUUAWJUUQ
      UVCTXDXEYEWKWEUSVFWLUUEVHVIVJVEVKUUNUUPUUSXDXEWMVMUIWSUVAUUMUUIKUUOWSYBUU
      IUUMUUKWNUUMXTUUHWOWPDXJAUMVTWAWBUJUOUOBCWTWQWR $.
      $( [7-Aug-2000] $)

    $( The orthogonal complement of a subspace is a subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    shocsh $p |- ( A e. SH -> ( _|_ ` A ) e. SH ) $=
      ( csh wcel chil wss cort cfv shss ocsh syl ) ABCADEAFGBCAHAIJ $.
      $( [10-Oct-1999] $)

    $( An orthogonal complement is a subset of Hilbert space. $)
    ocss $p |- ( A (_ H~ -> ( _|_ ` A ) (_ H~ ) $=
      ( chil wss cort cfv csh wcel ocsh shss syl ) ABCADEZFGKBCAHKIJ $.
      $( [9-Aug-2000] $)

    $( An orthogonal complement is a subset of Hilbert space. $)
    shocss $p |- ( A e. SH -> ( _|_ ` A ) (_ H~ ) $=
      ( csh wcel chil wss cort cfv shss ocss syl ) ABCADEAFGDEAHAIJ $.
      $( [11-Oct-1999] $)

    $( Contraposition law for orthogonal complement. $)
    occont $p |- ( ( A (_ H~ /\ B (_ H~ ) ->
                 ( A (_ B -> ( _|_ ` B ) (_ ( _|_ ` A ) ) ) $=
      ( vx vy chil wss wa cort cfv cv csp co cc0 wceq wral crab wi wcel wal
      ssel imim1d 19.20dv df-ral 3imtr4g a1d r19.21aiv ss2rab sylibr adantl
      ocvalt ad2antlr ad2antrr 3sstr4d ex ) AEFZBEFZGZABFZBHIZAHIZFUQURGCJZDJZK
      LMNZDBOZCEPZVCDAOZCEPZUSUTURVEVGFZUQURVDVFQZCEOVHURVICEURVIVAERURVBBRZVCQ
      ZDSVBARZVCQZDSVDVFURVKVMDURVLVJVCABVBTUAUBVCDBUCVCDAUCUDUEUFVDVFCEUGUHUIU
      PUSVENUOURCDBUJUKUOUTVGNUPURCDAUJULUMUN $.
      $( [8-Aug-2000] $)

    $( Double contraposition for orthogonal complement. $)
    occon2t $p |- ( ( A (_ H~ /\ B (_ H~ ) ->
              ( A (_ B -> ( _|_ ` ( _|_ ` A ) ) (_ ( _|_ ` ( _|_ ` B ) ) ) ) $=
      ( chil wss wa cort cfv occont wi ocss anim12i ancoms syl syld ) ACDZBCDZE
      ZABDBFGZAFGZDZSFGRFGDZABHQRCDZSCDZEZTUAIPOUDPUBOUCBJAJKLRSHMN $.
      $( [22-Jul-2001] $)

    ${
      occon2.1 $e |- A (_ H~ $.
      occon2.2 $e |- B (_ H~ $.
      $( Double contraposition for orthogonal complement. $)
      occon2 $p |- ( A (_ B ->
                   ( _|_ ` ( _|_ ` A ) ) (_ ( _|_ ` ( _|_ ` B ) ) ) $=
        ( chil wss cort cfv wi occon2t mp2an ) AEFBEFABFAGHGHBGHGHFICDABJK $.
        $( [9-Aug-2000] $)
    $}

    $( The zero vector belongs to an orthogonal complement of a Hilbert
       subspace. $)
    oc0 $p |- ( H e. SH -> 0h e. ( _|_ ` H ) ) $=
      ( csh wcel cort cfv c0v shocsh sh0 syl ) ABCADEZBCFJCAGJHI $.
      $( [11-Oct-1999] $)

    $( Members of a subset and its complement are orthogonal. $)
    ocorth $p |- ( H (_ H~ -> ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
                 ( A .ih B ) = 0 ) ) $=
      ( vx chil wss wcel cort cfv wa csp co cc0 wceq cv wral ocelt pm3.27bda
      adantl wi opreq2 eqeq1d rcla4v ad2antlr wb orthcom ssel2 ocss sseld imp
      syl2an sylibrd mpd anandis ex ) CEFZACGZBCHIZGZJABKLMNZUPUQUSUTUPUQJZUPUS
      JZJZBDOZKLZMNZDCPZUTVBVGVAUPUSBEGZVGDBCQRSVCVGBAKLZMNZUTUQVGVJTUPVBVFVJDA
      CVDANVEVIMVDABKUAUBUCUDAEGVHUTVJUEVAVBABUFCEAUGUPUSVHUPUREBCUHUIUJUKULUMU
      NUO $.
      $( [9-Aug-2000] $)

    $( Members of a subspace and its complement are orthogonal. $)
    shocorth $p |- ( H e. SH -> ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
                 ( A .ih B ) = 0 ) ) $=
      ( csh wcel chil wss cort cfv wa csp co cc0 wceq wi shss ocorth syl ) CDEC
      FGACEBCHIEJABKLMNOCPABCQR $.
      $( [10-Oct-1999] $)

    $( Inclusion in complement of complement.  Part of Proposition 1 of
       [Kalmbach] p. 65. $)
    ococss $p |- ( A (_ H~ -> A (_ ( _|_ ` ( _|_ ` A ) ) ) $=
      ( vy vx chil wss cort cfv cv wcel csp co cc0 wceq wral wa ssel ocorth
      exp3a r19.21adv jcad wb ocss ocelt syl sylibrd ssrdv ) ADEZBAAFGZFGZUGBHZ
      AIZUJDIZUJCHZJKLMZCUHNZOZUJUIIZUGUKULUOADUJPUGUKUNCUHUGUKUMUHIUNUJUMAQRST
      UGUHDEUQUPUAAUBCUJUHUCUDUEUF $.
      $( [9-Aug-2000] $)

    $( Inclusion in complement of complement.  Part of Proposition 1 of
       [Kalmbach] p. 65. $)
    shococss $p |- ( A e. SH -> A (_ ( _|_ ` ( _|_ ` A ) ) ) $=
      ( csh wcel chil wss cort cfv shss ococss syl ) ABCADEAAFGFGEAHAIJ $.
      $( [10-Oct-1999] $)

    $( Members of orthogonal subspaces are orthogonal. $)
    shorth $p |- ( H e. SH -> ( G (_ ( _|_ ` H ) ->
        ( ( A e. G /\ B e. H ) -> ( A .ih B ) = 0 ) ) ) $=
      ( csh wcel cort cfv wss wa csp co cc0 wceq shocorth imp chil wb shss
      sseld shocss anim12d orthcom syl mpbid ssel anim1d ancom sylib sylan2
      exp32 ) DEFZCDGHZIZACFZBDFZJZABKLMNZULUPAUMFZJZURUNUQJZULUTJZBAKLMNZURULU
      TVCBADOPVBBQFZAQFZJZVCURRULUTVFULUPVDUSVEULDQBDSTULUMQADUATUBPBAUCUDUEVAU
      SUPJZUTUNUQVGUNUOUSUPCUMAUFUGPUSUPUHUIUJUK $.
      $( [17-Oct-1999] $)

    $( Intersection of a Hilbert subspace and its complement.  Part of
       Proposition 1 of [Kalmbach] p. 65. $)
    ocin $p |- ( A e. SH -> ( A i^i ( _|_ ` A ) ) = 0H ) $=
      ( vx vy csh wcel cort cfv cin c0h cv wa c0v wceq chil csp co cc0 wral wi
      shocelt opreq2 eqeq1d rcla4cv his6t biimpd sylan9r syl6bi com23 imp3a
      eleq1 anbi12d sh0 oc0 jca syl5cbir impbid elin elch0 3bitr4g eqrdv ) ADEZ
      BAAFGZHZIVABJZAEZVDVBEZKZVDLMZVDVCEVDIEVAVGVHVAVEVFVHVAVFVEVHVAVFVDNEZVDC
      JZOPZQMZCARZKVEVHSCVDATVMVEVDVDOPZQMZVIVHVLVOCVDAVJVDMVKVNQVJVDVDOUAUBUCV
      IVOVHVDUDUEUFUGUHUIVHVGLAEZLVBEZKVAVHVEVPVFVQVDLAUJVDLVBUJUKVAVPVQAULAUMU
      NUOUPVDAVBUQVDURUSUT $.
      $( [11-Oct-1999] $)
  $}

  $( A nonzero vector in the complement of a subspace does not belong to the
     subspace. $)
  ocnelt $p |- ( ( H e. SH /\ A e. ( _|_ ` H ) /\ A =/= 0h ) -> -. A e. H ) $=
    ( csh wcel cort cfv c0v wne wn wa c0h wceq wi cin ocin eleq2d biimpd elin
    syl5ibr exp3a com23 imp elch0 syl6ib necon3ad 3impia ) BCDZABEFZDZAGHABDZIU
    GUIJZUJAGUKUJAKDZAGLUGUIUJULMUGUJUIULUGUJUIULUGABUHNZDZULUJUIJUGUNULUGUMKAB
    OPQABUHRSTUAUBAUCUDUEUF $.
    $( [11-May-2006] $) $( [10-Apr-2006] $)

  ${
    $d x y A $.
    chocval.1 $e |- A e. CH $.
    $( Value of the orthogonal complement of a Hilbert lattice element.  The
       orthogonal complement of ` A ` is the set of vectors that are
       orthogonal to all vectors in ` A ` . $)
    chocval $p |- ( _|_ ` A ) = { x e. H~ | A. y e. A ( x .ih y ) = 0 } $=
      ( chil wss cort cfv cv csp co cc0 wceq wral crab chssi ocvalt ax-mp ) CEF
      CGHAIBIJKLMBCNAEOMCDPABCQR $.
      $( [11-Aug-2004] $) $( [8-Aug-2004] $)
  $}

  ${
    chocuni.1 $e |- H e. CH $.
    $( Lemma for uniqueness part of Projection Theorem.  Theorem 3.7(i) of
       [Beran] p. 102 (uniqueness part). $)
    chocuni $p |- ( ( ( A e. H /\ B e. ( _|_ ` H ) ) /\
                    ( C e. H /\ D e. ( _|_ ` H ) ) ) ->
            ( ( R = ( A +h B ) /\ R = ( C +h D ) ) -> ( A = C /\ B = D ) ) ) $=
      ( wcel cort cfv wa cva co cmv wceq chil wb c0v pm3.26 anim1i hvsub4t
      syldan hvsubidt ad2antrr opreq1d hvsubclt hvaddid2t syl adantll 3eqtrd
      adantrl pm3.27 anim2i sylanc ad2antll opreq2d ax-hvaddid ancoms adantrr
      adantlr eqeq12d chel csh chshi shocsh ax-mp shel anim12i syl2an wi
      shsubcltOLD a1d ad2ant2r eleq1 syl5cbi ad2ant2l jcad hvsubeq0t eqcom
      syl6bb cin c0h ocin eleq2i elin elch0 3bitr3 syl5bb sylibd eleq1a sylbid
      eqtr2t syl5 ) AFHZBFIJZHZKZCFHZDWOHZKZKZABLMZADLMZNMZCDLMZXCNMZOZACOZBDOZ
      KZEXBOEXEOKZXAXGBDNMZCANMZOZXJAPHZBPHZKZCPHZDPHZKZXGXNQWQWTXQXTKXDXLXFXMX
      QXSXDXLOXRXQXSKZXDAANMZXLLMZRXLLMZXLXQXSXOXSKZXDYCOXQXOXSXOXPSTABADUAUBYA
      YBRXLLXOYBROXPXSAUCUDUEXPXSYDXLOZXOXPXSKXLPHYFBDUFXLUGUHUIUJUKXOXTXFXMOXP
      XOXTKZXFXMDDNMZLMZXMRLMZXMXTYEXFYIOYGCDADUAXOXTULXTXSXOXRXSULUMUNYGYHRXML
      XSYHROXOXRDUCUOUPXOXRYJXMOZXSXRXOYKXRXOKXMPHYKCAUFXMUQUHURUSUJUTVAWNXOWPX
      PAFGVBBWOFVCHZWOVCHZFGVDZFVEVFZVGVHZWRXRWSXSCFGVBDWOYOVGVHZVIXAXNXHXIXAXN
      XMFHZXMWOHZKZXHXAXNYRYSWNWRXNYRVJWPWSWNWRKZYRXNWRWNYRYLWRWNKYRVJYNCAFVKVF
      URZVLVMWPWSXNYSVJWNWRXNXLWOHZYSWPWSKZXLXMWOVNYMUUDUUCVJYOBDWOVKVFZVOVPVQX
      AXMROZXHYTXAUUFCAOZXHXQXTUUFUUGQZWQWTXOXRUUHXPXSXRXOUUHCAVRURVMYPYQVICAVS
      VTXMFWOWAZHXMWBHYTUUFUUIWBXMYLUUIWBOYNFWCVFZWDXMFWOWEXMWFWGWHWIXAXNXLFHZU
      UCKZXIXAXNUUKUUCWNWRXNUUKVJZWPWSUUAYRUUMUUBXMFXLWJUHVMWPWSXNUUCVJWNWRUUDU
      UCXNUUEVLVPVQXAXLROZXIUULXQXTUUNXIQZWQWTXPXSUUOXOXRBDVRVPYPYQVIXLUUIHXLWB
      HUULUUNUUIWBXLUUJWDXLFWOWEXLWFWGWHWIVQWKXKXBXEXCNEXBXEWLUEWM $.
      $( [23-Oct-1999] $)
  $}

  ${
    occllem1.1 $e |- A e. H~ $.
    occllem1.2 $e |- B e. H~ $.
    occllem1.3 $e |- S e. H~ $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem1 $p |-  ( abs ` ( ( B .ih S ) - ( A .ih S ) ) ) <_
                    ( ( normh ` ( B -h A ) ) x. ( normh ` S ) ) $=
      ( csp co cmin cabs cfv cmv cno cmul cle c1 cneg caddc csm cva chil wcel
      wceq ax1cn negcl hvmulcl ax-his2 mp3an cc ax-his3 opreq2i eqtr2 hicl
      mulm1 negsub hvsubval opreq1i 3eqtr4 fveq2i hvsubcl bcsHIL eqbrtr ) BCGHZACG
      HZIHZJKBALHZCGHZJKVFMKCMKNHOVEVGJVCPQZVDNHZRHZBVHASHZTHZCGHZVEVGVMVCVKCGH
      ZRHZVJBUAUBVKUAUBCUAUBZVMVOUCEVHAPUDUEZDUFFBVKCUGUHVNVIVCRVHUIUBAUAUBVPVN
      VIUCVQDFVHACUJUHUKULVJVCVDQZRHVEVIVRVCRVDACDFUMZUNUKVCVDBCEFUMVSUOULVFVLC
      GBAEDUPUQURUSVFCBAEDUTFVAVB $.
      $( [12-Oct-1999] $)
  $}

  ${
    occllem2.1 $e |- S e. H~ $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem2 $p |-  ( ( A e. H~ /\ B e. H~ ) ->
                  ( abs ` ( ( B .ih S ) - ( A .ih S ) ) ) <_
                  ( ( normh ` ( B -h A ) ) x. ( normh ` S ) ) ) $=
      ( chil wcel csp co cmin cabs cfv cmv cno cmul cle wbr c0v cif wceq
      opreq1 opreq2d fveq2d opreq2 opreq1d breq12d ax-hv0cl elimel occllem1
      dedth2h ) AEFZBEFZBCGHZACGHZIHZJKZBALHZMKZCMKZNHZOPULUJAQRZCGHZIHZJKZBUTL
      HZMKZURNHZOPUKBQRZCGHZVAIHZJKZVGUTLHZMKZURNHZOPABQQAUTSZUOVCUSVFOVNUNVBJV
      NUMVAULIAUTCGTUAUBVNUQVEURNVNUPVDMAUTBLUCUBUDUEBVGSZVCVJVFVMOVOVBVIJVOULV
      HVAIBVGCGTUDUBVOVEVLURNVOVDVKMBVGUTLTUBUDUEUTVGCAQEUFUGBQEUFUGDUHUI $.
      $( [13-Oct-1999] $)
  $}

  ${
    $d x y z F $.  $d x y z S $.  $d z G $.  $d x y D $.
    occllem3.1 $e |- G =
                    { <. x , y >. | ( x e. NN /\ y = ( ( F ` x ) .ih S ) ) } $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem3 $p |- ( D e. NN -> ( G ` D ) = ( ( F ` D ) .ih S ) ) $=
      ( cv cfv csp co cn wceq fveq2 opreq1d oprex fvopab4 ) ABCAHZEIZDJKCEIZDJK
      LFRCMSTDJRCENOGTDJPQ $.
      $( [13-Oct-1999] $)

    ${
      occllem4.2 $e |- S e. H~ $.
      $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
         3.12 of [Beran] p. 107. $)
      occllem4 $p |- ( F : NN --> H~ -> G : NN --> CC ) $=
        ( cn chil wf cv cfv csp co cc wcel wral wa ffvelrn jctir hiclt syl
        r19.21aiva fopab2 sylib ) HIDJZAKZDLZCMNZOPZAHQHOEJUFUJAHUFUGHPRZUHIPZC
        IPZRUJUKULUMHIUGDSGTUHCUAUBUCABHOUIEFUDUE $.
        $( [12-Oct-1999] $)
    $}

    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem5 $p |- ( A. z e. NN ( ( F ` z ) .ih S ) = 0 -> G ~~> 0 ) $=
      ( cv cfv csp co cc0 wceq cn wral csn cxp cli wcel wa copab wb fveq2
      opreq1d eqeq1d rcla4v eqeq2 syl6com pm5.32d opabbidv fconstopab 3eqtr4g
      c1 cuz nnuz xpeq1 ax-mp 1z climuz0 eqbrtr syl6eqbr ) CHZEIZDJKZLMZCNOZFNL
      PZQZLRVFAHZNSZBHZVIEIZDJKZMZTZABUAVJVKLMZTZABUAFVHVFVOVQABVFVJVNVPVJVFVML
      MZVNVPUBVEVRCVINVBVIMZVDVMLVSVCVLDJVBVIEUCUDUEUFVMLVKUGUHUIUJGABNLUKULVHU
      MUNIZVGQZLRNVTMVHWAMUONVTVGUPUQUMURUSUTVA $.
      $( [6-Jul-2006] $) $( [12-Oct-1999] $)
  $}

  ${
    $d z w v u A $.  $d x y z w v u F $.  $d w u v G $.  $d x y z w v u S $.
    occllem6.1 $e |- G =
                    { <. x , y >. | ( x e. NN /\ y = ( ( F ` x ) .ih S ) ) } $.
    occllem6.2 $e |- A e. H~ $.
    occllem6.3 $e |- S e. H~ $.
    occllem6.4 $e |- F e. V $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem6 $p |-  ( S =/= 0h -> ( F ~~>v A -> G ~~> ( A .ih S ) ) ) $=
      ( vv vu vw vz c0v wne chli wbr csp co cc wcel cv cfv cn wral wa cc0 clt
      cle cmin cabs wi wrex cr cli chil wf cmv cno ffvelrn occllem4 sylan
      r19.21aiva hicl jctil adantr a1i adantrd cdiv normcl redivclt mp3an2
      gt0ne0 sylan2 adantlr divgt0t mpanr1 jca expcom imim1d cmul wb ltmuldivt
      mp3anl2 jctir hvsubclt normclt 3syl adantll pm3.26 ad2antlr ad2antrr
      sylanc occllem2 syl occllem3 opreq1d fveq2d breq1d adantl mpbird lelttrt
      3expa subclt absclt axmulrcl mpand adantrr an1rs sylbird imim2d
      r19.20dva r19.22sdv ex a2d syld exp4a normgt0t ax-mp sylanb wceq breq2
      imbi2d rexralbidv imbi12d rcla4cv imp3a syl5 r19.21adv jcad elisseti
      hlim syl5ib cin c1 1z cuz nnuz eqimss2i nnssz nnex fopabex2 clm4 wss
      inss2 ssralv biimpar syl6 ) DOPZECQRZCDSTZUAUBZKUCZFUDZUAUBZKUEUFZUGZUHLU
      CZUIRZMUCUUNUJRZUUOUULUKTZULUDZUUSUIRZUMZKUEUFZMUEUNZUMZLUOUFZUGZFUULUPRZ
      UUJUEUQEURZCUQUBZUGZUHNUCZUIRZUVAUUNEUDZCUSTZUTUDZUVOUIRZUMZKUEUFMUEUNZUM
      ZNUOUFZUGZUVJUUKUUJUWEUURUVIUUJUVNUURUWDUVNUURUMUUJUVLUURUVMUVLUUQUUMUVLU
      UPKUEUEUAFURUUNUEUBZUUPUVLUEUAUUNFVAABDEFGIVBVCZVDCDHIVEZVFVGVHVIUUJUVNUW
      DUVIUUJUVNUWDUVIUMUUJUVNUGZUWDUVHLUOUWIUUSDUTUDZVJTZUOUBZUHUWKUIRZUGZUVAU
      VSUWKUIRZUMZKUEUFZMUEUNZUMZUUSUOUBZUVHUMZUWDUUJUVLUWSUXAUMZUVMUHUWJUIRZUV
      LUXBUUJUXCUVLUGZUWSUWTUUTUVGUXDUWSUWTUUTUGZUWRUMUXEUVGUMUXDUXEUWNUWRUXCUX
      EUWNUMUVLUXEUXCUWNUXEUXCUGUWLUWMUWTUXCUWLUUTUWTUWJUHPZUWLUXCUWTUWJUOUBZUX
      FUWLDIVKZUUSUWJVLVMUWJUXHVNVOVPUXEUXGUXCUWMUXHUUSUWJVQVRVSVTVGWAUXDUXEUWR
      UVGUXDUXEUWRUVGUMUXDUXEUGZUWQUVFMUEUXIUWPUVEKUEUXIUWFUGZUWOUVDUVAUXJUWOUV
      SUWJWBTZUUSUIRZUVDUVSUOUBZUWTUGUXCUXLUWOWCZUXJUXMUXGUWTUXCUXNUXHUVSUWJUUS
      WDWEUXJUXMUWTUXDUWFUXMUXEUVLUWFUXMUXCUVLUWFUGZUVQUQUBZUVMUGUVRUQUBUXMUXOU
      XPUVMUEUQUUNEVAZHWFUVQCWGUVRWHWIZWJVPUXEUWTUXDUWFUWTUUTWKWLVSUXDUXCUXEUWF
      UXCUVLWKWMWNUXDUWFUXEUXLUVDUMZUXDUWFUGZUWTUXSUUTUXTUWTUGUVCUXKUJRZUXLUVDU
      XTUYAUWTUVLUWFUYAUXCUXOUYAUVQDSTZUULUKTZULUDZUXKUJRZUXOUVMUXPUGUYEUXOUXPU
      VMUXQHVFCUVQDIWOWPUWFUYAUYEWCUVLUWFUVCUYDUXKUJUWFUVBUYCULUWFUUOUYBUULUKAB
      UUNDEFGWQWRWSWTXAXBWJVGUVCUOUBZUXKUOUBZUGZUWTUYAUXLUGUVDUMZUXTUYFUYGUWTUY
      IUVCUXKUUSXCXDUVLUWFUYHUXCUXOUYFUYGUXOUUPUUMUGUVBUAUBUYFUXOUUPUUMUWGUWHWF
      UUOUULXEUVBXFWIUXOUXMUXGUGUYGUXOUXMUXGUXRUXHWFUVSUWJXGWPVSWJVCXHXIXJXKXLX
      MXNXOXPXQXRDUQUBUUJUXCWCIDXSXTYAXIUWDUWLUWMUWRUWCUWMUWRUMNUWKUOUVOUWKYBZU
      VPUWMUWBUWRUVOUWKUHUIYCUYJUWAUWPMKUEUEUYJUVTUWOUVAUVOUWKUVSUIYCYDYEYFYGYH
      YIYJXOYHYKNMKCEJCUQHYLYMYNUURUVKUVIUUMUUPKUEUEYOZUFZUVKUVIWCUUQLUULMKFYPY
      PUEUEYQUEYPYRUDYSYTZUUAYQUYMUUAABUEAUCEUDDSTFUUBGUUCUUDUYKUEUUEUUQUYLUMUE
      UEUUFUUPKUYKUEUUGXTVOUUHUUI $.
      $( [17-Nov-2007] $) $( [12-Oct-1999] $)
  $}

  ${
    $d x y z F $.  $d x y z S $.
    occllem7.1 $e |- A e. H~ $.
    occllem7.2 $e |- S e. H~ $.
    occllem7.3 $e |- F e. V $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem7 $p |- ( ( F ~~>v A /\
               A. x e. NN ( ( F ` x ) .ih S ) = 0 ) -> ( A .ih S ) = 0 ) $=
      ( vy vz chli wbr cv cfv csp co cc0 wceq cn wral wa wi c0v opreq2 chil
      wcel hi02t ax-mp syl6eq a1d wne copab cli eqid occllem6 occllem5 a1i
      anim12d oprex cr 0re elisseti climuni syl6 pm2.61ine ) DBJKZALDMCNOPQARSZ
      TZBCNOZPQZUACUBCUBQZVIVGVJVHBUBNOZPCUBBNUCBUDUEVKPQEBUFUGUHUICUBUJZVGHLZR
      UEILVMDMCNOQTHIUKZVHULKZVNPULKZTVIVLVEVOVFVPHIBCDVNVNUMZEFGUNVFVPUAVLHIAC
      DVNVQUOUPUQVHPVNBCNURPUSUTVAVBVCVD $.
      $( [6-Jul-2006] $) $( [12-Oct-1999] $)
  $}

  ${
    $d x y z F $.  $d x y z S $.
    occllem8.1 $e |- F e. V $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem8 $p |- ( ( A e. H~ /\ S e. H~ ) -> ( ( F ~~>v A /\
             A. x e. NN ( ( F ` x ) .ih S ) = 0 ) -> ( A .ih S ) = 0 ) ) $=
      ( chil wcel chli wbr cv cfv csp co cc0 wceq cn wral wa wi c0v cif breq2
      anbi1d opreq1 eqeq1d imbi12d opreq2 ralbidv anbi2d ax-hv0cl elimel
      occllem7 dedth2h ) BFGZCFGZDBHIZAJDKZCLMZNOZAPQZRZBCLMZNOZSDUNBTUAZHIZUTR
      ZVDCLMZNOZSVEUQUOCTUAZLMZNOZAPQZRZVDVILMZNOZSBCTTBVDOZVAVFVCVHVPUPVEUTBVD
      DHUBUCVPVBVGNBVDCLUDUEUFCVIOZVFVMVHVOVQUTVLVEVQUSVKAPVQURVJNCVIUQLUGUEUHU
      IVQVGVNNCVIVDLUGUEUFAVDVIDBTFUJUKCTFUJUKEULUM $.
      $( [13-Oct-1999] $)
  $}

  ${
    $d x y z f A $.
    occl.1 $e |- A (_ H~ $.
    $( Closure of complement of Hilbert subset.  Part of Remark 3.12 of
       [Beran] p. 107. $)
    occl $p |- ( _|_ ` A ) e. CH $=
      ( vf vx vy vz cort cfv cch wcel csh cn cv wf chli wbr wa wi wal
      closedsub chil wss ocsh ax-mp csp co cc0 wceq wral visset hlimvec adantl
      occllem8 exp4b sseli syl5 com23 mpcom imp r19.20dva ffvelrn wb ocelt
      pm3.27bi syl r19.21aiva ralcom sylibr impcom jca gen2 mpbir2an ) AGHZIJVM
      KJZLVMCMZNZVODMZOPZQZVQVMJZRZDSCSDCVMTAUAUBZVNBAUCUDWACDVSVQUAJZVQEMZUEUF
      UGUHZEAUIZQZVTVSWCWFVRWCVPVQVOCUJZDUJUKZULVRVPWFVRFMZVOHZWDUEUFUGUHZFLUIZ
      EAUIZWFVPVRWMWEEAVRWDAJZWMWERZWCVRWOWPRWIWCWOVRWPWCWDUAJZVRWPRWOWCWQVRWMW
      EFVQWDVOWHUMUNAUAWDBUOUPUQURUSUTVPWLEAUIZFLUIWNVPWRFLVPWJLJQWKVMJZWRLVMWJ
      VOVAWSWKUAJZWRWBWSWTWRQVBBEWKAVCUDVDVEVFWLEFALVGVHUPVIVJWBVTWGVBBEVQAVCUD
      VHVKVL $.
      $( [7-Aug-2000] $)
  $}

  $( Closure of complement of Hilbert subset.  Part of Remark 3.12 of
     [Beran] p. 107. $)
  occlt $p |- ( A (_ H~ -> ( _|_ ` A ) e. CH ) $=
    ( chil wss cort cfv cch wcel cif wceq fveq2 eleq1d sseq1 ssid elimhyp occl
    dedth ) ABCZADEZFGQABHZDEZFGABASIRTFASDJKSQSBCBBCABASBLBSBLBMNOP $.
    $( [8-Aug-2000] $)

  $( Closure of complement of Hilbert subspace.  Part of Remark 3.12 of
     [Beran] p. 107. $)
  shocclt $p |- ( A e. SH -> ( _|_ ` A ) e. CH ) $=
    ( csh wcel chil wss cort cfv cch shss occlt syl ) ABCADEAFGHCAIAJK $.
    $( [13-Oct-1999] $)

  $( Closure of complement of Hilbert subspace.  Part of Remark 3.12 of
     [Beran] p. 107. $)
  chocclt $p |- ( A e. CH -> ( _|_ ` A ) e. CH ) $=
    ( cch wcel csh cort cfv chsh shocclt syl ) ABCADCAEFBCAGAHI $.
    $( [22-Jul-2001] $)

  ${
    choccl.1 $e |- A e. CH $.
    $( Closure of ` CH ` orthocomplement. $)
    choccl $p |- ( _|_ ` A ) e. CH $=
      ( chssi occl ) AABCD $.
      $( [29-Jul-1999] $)
  $}

