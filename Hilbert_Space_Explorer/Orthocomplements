$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Orthocomplements
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Define orthogonal complement of a subset (usually a subspace) of Hilbert
       space.  The orthogonal complement is the set of all vectors orthogonal
       to all vectors in the subset.  See ~ ocval and ~ chocvali for its
       value.  Textbooks usually denote this unary operation with the symbol
       ` _|_ ` as a small superscript, although Mittelstaedt uses the symbol as
       a prefix operation.  Here we define a function (prefix operation)
       ` _|_ ` rather than introducing a new syntactical form.  This lets us
       take advantage of the theorems about functions that we already have
       proved under set theory.  Definition of [Mittelstaedt] p. 9. $)
    df-oc $a |- _|_ = { <. x , y >. | ( x C_ ~H /\ y =
                         { z e. ~H | A. w e. x ( z .ih w ) = 0 } ) } $.
  $}

  $( Define the zero for closed subspaces of Hilbert space.  See ~ h0elch for
     closure law. $)
  df-ch0 $a |- 0H = { 0h } $.

  $( Membership in zero for closed subspaces of Hilbert space. $)
  elch0 $p |- ( A e. 0H <-> A = 0h ) $=
    ( c0h wcel c0v csn wceq df-ch0 eleq2i chil ax-hv0cl elisseti elsnc2 bitri
    ) ABCADEZCADFBNAGHADDIJKLM $.
    $( [6-Apr-2001] $)

  $( The zero subspace is a closed subspace.  Part of Proposition 1 of
     [Kalmbach] p. 65. $)
  h0elch $p |- 0H e. CH $=
    ( c0h c0v csn cch df-ch0 hsn0elch eqeltri ) ABCDEFG $.
    $( [30-May-1999] $)

  $( The zero subspace is a subspace of Hilbert space. $)
  h0elsh $p |- 0H e. SH $=
    ( c0h h0elch chshii ) ABC $.
    $( [2-Jun-2004] $)

  ${
    hhss.1 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    $( The vector addition operation on a subspace. $)
    hhssva $p |- ( +h |` ( H X. H ) ) = ( +v ` W ) $=
      ( cpv cfv c1st cva cxp cres csm cc cop eqid vafval cno fveq2i opex op1st
      eqtri cabl wcel cvv hilabl resexg ax-mp 3eqtrri ) BDEZBFEZFEGAAHZIZJKAHIZ
      LZFEUJBUGUGMNUHULFUHULOAIZLZFEULBUNFCPULUMUJUKQRSPUJUKGTUAUJUBUAUCGUITUDU
      ERUF $.
      $( [8-Apr-2008] $)

    $( The scalar multiplication operation on a subspace. $)
    hhsssm $p |- ( .h |` ( CC X. H ) ) = ( .s ` W ) $=
      ( cns cfv c1st c2nd cva cxp cres csm cc cop eqid smfval cno fveq2i opex
      op1st eqtri cabl wcel cvv hilabl resexg ax-mp hvmulex op2nd 3eqtrri ) BDE
      ZBFEZGEHAAIZJZKLAIZJZMZGEUOUJBUJNOUKUPGUKUPPAJZMZFEUPBURFCQUPUQUMUORSTQUM
      UOHUAUBUMUCUBUDHULUAUEUFKUCUBUOUCUBUGKUNUCUEUFUHUI $.
      $( [8-Apr-2008] $)

    $( The norm operation on a subspace. $)
    hhssnm $p |- ( normh |` H ) = ( norm ` W ) $=
      ( cnm cfv c2nd cva cxp cres csm cc cop cno eqid nmfval fveq2i opex cvv
      wcel chil cr wf normf ax-hilex fex mp2an resexg ax-mp op2nd 3eqtrri ) BDE
      ZBFEGAAHIZJKAHIZLZMAIZLZFEUOBUKUKNOBUPFCPUNUOULUMQMRSZUORSTUAMUBTRSUQUCUD
      TUARMUEUFMARUGUHUIUJ $.
      $( [8-Apr-2008] $)
  $}

  ${
    $d x y H $.
    hhssabl.1 $e |- H e. SH $.
    $( Abelian group property of subspace addition. $)
    hhssabli $p |- ( +h |` ( H X. H ) ) e. Abel $=
      ( vx vy cva cxp cres cgr wcel wss csubg cfv w3a c0v csm c2nd c1 cneg csn
      cvv ccnv ccom chil cabl hilabl ablgrp ax-mp cop cno eqid hhba hhva
      bafval hh0v 0vfval cnv cgn wceq hhnv hhsm invfval shssii csh cv co
      shaddcl mp3an1 sh0 cc wfn wf ax-hfvmul ffn ax1cn negcli curry1val
      mp3an12 shmulcl eqeltrd issubgi issubg mpbi simp2i cdm xpss12 mp2an
      ax-hfvadd fdmi sseqtr4i ssdmres wa ax-hvcom sheli syl2an oprvres ancoms
      3eqtr4d isabli ) CDEAAFZGZAEHIZWTHIZWTEJZWTEKLIXAXBXCMCDNEWTOPQRZSTFGUAUB
      ZUCAEUDIXAUEEUFUGEOUHUIUHZEUCXFXFUJZUKXFXGULZUMXFENXHXFXGUNUOXFUPIXEEUQLU
      RXFXGUSOXFEXEXHXFXGUTXEUJZVAUGABVBZWTUJAVCIZCVDZAIZDVDZAIZXLXNEVEZAIBXLXN
      AVFVGXKNAIBAVHUGXMXLXELZXDXLOVEZAOVIUCFZVJZXDVIIZXMXQXRURXSUCOVKXTVLXSUCO
      VMUGQVNVOZVIUCXDXLAOXEXIVPVQXKYAXMXRAIBYBXDXLAVRVQVSVTEWTWAWBWCWSEWDZJWTW
      DWSURWSUCUCFZYCAUCJZYEWSYDJXJXJAUCAUCWEWFYDUCEWGWHWIWSEWJWBXMXOWKXPXNXLEV
      EZXLXNWTVEXNXLWTVEZXLUCIXNUCIXPYFURXMXOXLXNWLXLABWMXNABWMWNXLXNAAEWOXOXMY
      GYFURXNXLAAEWOWPWQWR $.
      $( [9-Apr-2008] $)
  $}

  $( Abelian group property of subspace addition. $)
  hhssabl $p |- ( H e. SH -> ( +h |` ( H X. H ) ) e. Abel ) $=
    ( csh wcel cva cxp cres cabl chil cif wceq xpeq1 xpeq2 eqtrd reseq2d
    eleq1d helsh elimel hhssabli dedth ) ABCZDAAEZFZGCDTAHIZUCEZFZGCAHAUCJZUBUE
    GUFUAUDDUFUAUCAEUDAUCAKAUCUCLMNOUCAHBPQRS $.
    $( [9-Apr-2008] $)

  ${
    $d x y z H $.
    hhssnvt.1 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    ${
      hhssnv.2 $e |- H e. SH $.
      $( Normed complex vector space property of a subspace. $)
      hhssnv $p |- W e. NrmCVec $=
        ( vx vy vz csm cc cxp cres cva cno c0v cabl wcel cgr hhssabli ablgrp
        ax-mp cdm wss wceq chil shssii xpss12 mp2an ax-hfvadd fdmi sseqtr4i
        ssdmres mpbi grprn cgi cfv co csh sh0 oprvres ax-hv0cl hvaddid2i eqtri
        wb eqid grpid mpbir cop wf wfn crn df-f ax-hfvmul ffn ssid fnssres cv
        wrex oprvelrn wa eleq1 shmulcl mp3an1 eqeltrd syl5cbi r19.23aivv sylbi
        ssriv mpbir2an c1 ax1cn mpan sheli ax-hvmulid syl eqtrd w3a
        ax-hvdistr1 id syl3an 3adant1 opreq2d shaddcl sylan2 3impb 3adant3
        3adant2 opreq12d syl11anc 3eqtr4d caddc ax-hvdistr2 syl3an3 addcl
        sylan 3impa cmul ax-hvmulass mulcl isvci cr normf fssres cc0 fvres
        eqeq1d norm-i bitrd biimpa cabs norm-iii fveq2d adantl cle wbr norm-ii
        syl2an opreqan12d 3brtr4d isnvi ) EFHIAJZKZBLAAJZKZMAKZANUUCAUUCOPUUCQP
        ZADRZUUCSTZUUBLUAZUBUUCUAUUBUCUUBUDUDJZUUHAUDUBZUUJUUBUUIUBADUEZUUKAUDA
        UDUFUGUUIUDLUHUIUJUUBLUKULZUMZNUUCUNUOZUCZNNUUCUPZNUCZUUPNNLUPZNNAPZUUS
        UUPUURUCAUQPZUUSDAURTZUVANNAALUSUGNUTVAVBUUEUUSUUOUUQVCUUGUVANUUNUUCAUU
        MUUNVDVEUGVFEFGUUAUUCUUCUUAVGZAUUFUULYTAUUAVHUUAYTVIZUUAVJZAUBYTAUUAVKH
        IUDJZVIZYTUVEUBZUVCUVEUDHVHUVFVLUVEUDHVMTIIUBUUJUVGIVNUUKIIAUDUFUGUVEYT
        HVOUGZGUVDAGVPZUVDPZEVPZFVPZUUAUPZUVIUCZFAVQEIVQZUVIAPZUVCUVJUVOVCUVHEF
        IAUVIUUAVRTUVNUVPEFIAUVNUVMAPUVPUVKIPZUVLAPZVSZUVMUVIAVTUVSUVMUVKUVLHUP
        ZAUVKUVLIAHUSUUTUVQUVRUVTAPDUVKUVLAWAWBWCWDWEWFWGWHUVKAPZWIUVKUUAUPZWIU
        VKHUPZUVKWIIPUWAUWBUWCUCWJWIUVKIAHUSWKUWAUVKUDPZUWCUVKUCUVKADWLZUVKWMWN
        WOUVLIPZUWAUVPWPZUVLUVKUVILUPZHUPZUVLUVKHUPZUVLUVIHUPZLUPZUVLUVKUVIUUCU
        PZUUAUPZUVLUVKUUAUPZUVLUVIUUAUPZUUCUPZUWFUWDUVIUDPUWIUWLUCUWFUWAUVPUVLU
        VKUVIWQUWFWRUWEUVIADWLWSUWGUWNUVLUWHUUAUPZUWIUWGUWMUWHUVLUUAUWAUVPUWMUW
        HUCUWFUVKUVIAALUSWTXAUWFUWAUVPUWRUWIUCZUWFUWHAPZUWSUWAUVPVSUVLUWHIAHUSU
        UTUWAUVPUWTDUVKUVIAXBWBXCXDWOUWGUWQUWJUWKUUCUPZUWLUWGUWOUWJUWPUWKUUCUWF
        UWAUWOUWJUCZUVPUVLUVKIAHUSZXEUWFUVPUWPUWKUCUWAUVLUVIIAHUSXFXGUWGUWJAPZU
        WKAPZUXAUWLUCUWFUWAUXDUVPUUTUWFUWAUXDDUVLUVKAWAWBZXEUWFUVPUXEUWAUUTUWFU
        VPUXEDUVLUVIAWAWBXFUWJUWKAALUSXHWOXIUWFUVIIPZUWAWPZUVLUVIXJUPZUVKHUPZUW
        JUVIUVKHUPZLUPZUXIUVKUUAUPZUWOUVIUVKUUAUPZUUCUPZUWFUXGUWDUXJUXLUCUWAUVL
        UVIUVKXKUWEXLUWFUXGUWAUXMUXJUCZUXIIPUWAUXPUWFUXGVSZUXIUVKIAHUSUVLUVIXMX
        NXOUXHUXOUWJUXKUUCUPZUXLUXHUWOUWJUXNUXKUUCUWFUWAUXBUXGUXCXFUXGUWAUXNUXK
        UCUWFUVIUVKIAHUSWTZXGUXHUXDUXKAPZUXRUXLUCUWFUWAUXDUXGUXFXFUXGUWAUXTUWFU
        UTUXGUWAUXTDUVIUVKAWAWBZWTUWJUXKAALUSXHWOXIUXHUVLUVIXPUPZUVKHUPZUVLUXKH
        UPZUYBUVKUUAUPZUVLUXNUUAUPZUWFUXGUWDUYCUYDUCUWAUVLUVIUVKXQUWEXLUWFUXGUW
        AUYEUYCUCZUYBIPUWAUYGUXQUYBUVKIAHUSUVLUVIXRXNXOUXHUYFUVLUXKUUAUPZUYDUXH
        UXNUXKUVLUUAUXSXAUWFUXGUWAUYHUYDUCZUWFUXTUYIUXGUWAVSUVLUXKIAHUSUYAXCXDW
        OXIUVBVDXSUDXTMVHUUJAXTUUDVHYAUUKUDXTAMYBUGUWAUVKUUDUOZYCUCZUVKNUCZUWAU
        YKUVKMUOZYCUCZUYLUWAUYJUYMYCUVKAMYDZYEUWAUWDUYNUYLVCUWEUVKYFWNYGYHUWFUW
        AVSZUWJMUOZUVLYIUOZUYMXPUPZUWOUUDUOZUYRUYJXPUPUWFUWDUYQUYSUCUWAUVLUVKYJ
        UWEXCUYPUYTUWJUUDUOZUYQUYPUWOUWJUUDUXCYKUYPUXDVUAUYQUCUXFUWJAMYDWNWOUYP
        UYJUYMUYRXPUWAUYJUYMUCUWFUYOYLXAXIUWAUVRVSZUVKUVLLUPZMUOZUYMUVLMUOZXJUP
        ZUVKUVLUUCUPZUUDUOZUYJUVLUUDUOZXJUPYMUWDUVLUDPVUDVUFYMYNUWAUVRUVKUVLYOU
        WEUVLADWLYPVUBVUHVUCUUDUOZVUDVUBVUGVUCUUDUVKUVLAALUSYKVUBVUCAPZVUJVUDUC
        UUTUWAUVRVUKDUVKUVLAXBWBVUCAMYDWNWOUWAUVRUYJUYMVUIVUEXJUYOUVLAMYDYQYRCY
        S $.
        $( [26-Mar-2008] $)
    $}

    $( Normed complex vector space property of a subspace. $)
    hhssnvt $p |- ( H e. SH -> W e. NrmCVec ) $=
      ( csh wcel cnv cva c0h cif cxp cres csm cc cop cno wceq xpeq1 xpeq2
      eqtrd reseq2d opeq12d reseq2 syl5eq eleq1d eqid h0elsh elimel hhssnv
      dedth ) ADEZBFEGUJAHIZUKJZKZLMUKJZKZNZOUKKZNZFEAHAUKPZBURFUSGAAJZKZLMAJZK
      ZNZOAKZNURBUSVDUPVEUQUSVAUMVCUOUSUTULGUSUTUKAJULAUKAQAUKUKRSTUSVBUNLAUKMR
      TUAAUKOUBUACUCUDUKURURUEAHDUFUGUHUI $.
      $( [9-Apr-2008] $)
  $}

  ${
    $d x y z H $.
    hhsst.1 $e |- U = <. <. +h , .h >. , normh >. $.
    hhsst.2 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    $( A member of ` SH ` is a subspace. $)
    hhsst $p |- ( H e. SH -> W e. ( SubSp ` U ) ) $=
      ( csh wcel cnv cva cxp cres wss csm cc cno w3a wa css cfv hhssnvt resss
      3pm3.2i jctir wb hhnv hhva hhssva hhsm hhsssm hhnm hhssnm eqid isssp
      ax-mp sylibr ) BFGZCHGZIBBJZKZILZMNBJZKZMLZOBKZOLZPZQZCARSZGZUPUQVFBCETUT
      VCVEIURUAMVAUAOBUAUBUCAHGVIVGUDADUEVBMAUSIVHVDOCADUFBCEUGADUHBCEUIADUJBCE
      UKVHULUMUNUO $.
      $( [6-Apr-2008] $)

    ${
      hhssp3.3 $e |- W e. ( SubSp ` U ) $.
      hhssp3.4 $e |- H C_ ~H $.
      $( Lemma for ~ hhsssh . $)
      hhshsslem1 $p |- H = ( BaseSet ` W ) $=
        ( cba cfv cpv crn eqid bafval cdm cxp cgr wcel wceq cnv css hhnv sspnv
        mp2an nvgrp ax-mp grprndm cva cres csm cc cop cno fveq2i c1st vafval
        opex op1st cabl cvv hilabl resexg eqtri dmeqi wss chil xpss12
        ax-hfvadd fdmi sseqtr4i ssdmres mpbi dmxpid 3eqtri eqcomi ) CHIZBVOCJIZ
        KZBCVPVOVOLVPLZMVQVPNZNZBBOZNBVPPQZVQVTRCSQZWBASQCATIZQWCADUAFAWDCWDLUB
        UCCVPVRUDUEVPUFUEVSWAVSUGWAUHZNZWAVPWEVPWEUIUJBOUHZUKZULBUHZUKZJIZWECWJ
        JEUMWKWJUNIZUNIZWEWJWKWKLUOWMWHUNIWEWLWHUNWHWIWEWGUPUQUMWEWGUGURQWEUSQU
        TUGWAURVAUEUQVBVBVBVCWAUGNZVDWFWARWAVEVEOZWNBVEVDZWPWAWOVDGGBVEBVEVFUCW
        OVEUGVGVHVIWAUGVJVKVBVCBVLVMVBVN $.
        $( [10-Apr-2008] $)

      $( Lemma for ~ hhsssh . $)
      hhshsslem2 $p |- H e. SH $=
        ( vx vy csh wcel chil wss c0v wa cv cva co wral csm cc sh cn0v cfv cnv
        css wceq hhnv hh0v eqid sspz mp2an cba sspnv nvzcl ax-mp hhshsslem1
        eqcomi eleqtri eqeltrri pm3.2i cpv hhva sspgval mpanl12 nvgcl mp3an1
        eqeltrrd rgen2a cns hhsm sspsval nvscl rgen2 mpbir2an ) BJKBLMZNBKZOHPZ
        IPZQRZBKZIBSHBSZVRVSTRZBKZIBSHUASZOHIBUBVPVQGCUCUDZNBAUEKZCAUFUDZKZWFNU
        GADUHZFWFAWHCNADUIWFUJZWHUJZUKULWFCUMUDZBCUEKZWFWMKWGWIWNWJFAWHCWLUNULZ
        CWMWFWMUJWKUOUPBWMABCDEFGUQZURUSUTVAWBWEWAHIBVRBKZVSBKZOZVRVSCVBUDZRZVT
        BWGWIWSXAVTUGWJFVRVSAWTQWHCBWPADVCWTUJZWLVDVEWNWQWRXABKWOVRVSCWTBWPXBVF
        VGVHVIWDHIUABVRUAKZWROZVRVSCVJUDZRZWCBWGWIXDXFWCUGWJFVRVSXETAWHCBWPADVK
        XEUJZWLVLVEWNXCWRXFBKWOVRVSXECBWPXGVMVGVHVNVAVO $.
        $( [6-Apr-2008] $)
    $}

    $( The predicate " ` H ` is a subspace of Hilbert space." $)
    hhsssh $p |- ( H e. SH <-> ( W e. ( SubSp ` U ) /\ H C_ ~H ) ) $=
      ( csh wcel css cfv chil wss wa hhsst shss jca cif eleq1 cva cxp cres csm
      cc cop cno eqid wceq xpeq1 xpeq2 eqtrd reseq2d opeq12d reseq2 syl5eq
      eleq1d sseq1 anbi12d wfn wf ax-hfvadd ffn ax-mp fnresdm ax-hfvmul
      opeq12i cr normf eqtr4i cnv hhnv sspid eqeltri ssid pm3.2i elimhyp
      simpli simpri hhshsslem2 dedth impbii ) BFGZCAHIZGZBJKZLZVTWBWCABCDEMBNOW
      DVTWDBJPZFGBJBWEFQAWERWEWESZTZUAUBWESZTZUCZUDWETZUCZDWLUEWLWAGZWEJKZWDWMW
      NLRJJSZTZUAUBJSZTZUCZUDJTZUCZWAGZJJKZLBJBWEUFZWBWMWCWNXDCWLWAXDRBBSZTZUAU
      BBSZTZUCZUDBTZUCWLCXDXIWJXJWKXDXFWGXHWIXDXEWFRXDXEWEBSWFBWEBUGBWEWEUHUIUJ
      XDXGWHUABWEUBUHUJUKBWEUDULUKEUMUNBWEJUOUPJWEUFZXBWMXCWNXKXAWLWAXKWSWJWTWK
      XKWPWGWRWIXKWOWFRXKWOWEJSWFJWEJUGJWEWEUHUIUJXKWQWHUAJWEUBUHUJUKJWEUDULUKU
      NJWEJUOUPXBXCXAAWAXARUAUCZUDUCAWSXLWTUDWPRWRUARWOUQZWPRUFWOJRURXMUSWOJRUT
      VAWORVBVAUAWQUQZWRUAUFWQJUAURXNVCWQJUAUTVAWQUAVBVAVDUDJUQZWTUDUFJVEUDURXO
      VFJVEUDUTVAJUDVBVAVDDVGAVHGAWAGADVIAWAWAUEVJVAVKJVLVMVNZVOWMWNXPVPVQVRVS
      $.
      $( [25-Mar-2008] $)
  $}

  ${
    $d x y H $.
    hhsssh2.1 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    $( The predicate " ` H ` is a subspace of Hilbert space." $)
    hhsssh2 $p |- ( H e. SH <-> ( W e. NrmCVec /\ H C_ ~H ) ) $=
      ( csh wcel cva csm cop cno css cfv chil wss wa cnv eqid hhsssh cxp cres
      cc w3a wb hhnv hhva cpv fveq2i c1st vafval opex op1st cabl cvv hilabl
      resexg ax-mp eqtri eqtr2i hhsm cns c2nd smfval hvmulex op2nd hhnm cnm
      nmfval cr wf normf ax-hilex fex mp2an isssp resss 3pm3.2i mpbiran2
      anbi1i bitri ) ADEBFGHIHZJKZEZALMZNBOEZWBNVSABVSPZCQWAWCWBWAWCFAARZSZFMZG
      TARZSZGMZIASZIMZUAZVSOEWAWCWMNUBVSWDUCWIGVSWFFVTWKIBVSWDUDBUEKWFWIHZWKHZU
      EKZWFBWOUECUFWPWOUGKZUGKZWFWOWPWPPUHWRWNUGKWFWQWNUGWNWKWFWIUIZUJZUFWFWIFU
      KEWFULEUMFWEUKUNUOZUJUPUPUQVSWDURBUSKWOUSKZWIBWOUSCUFXBWQUTKZWIXBWOXBPVAX
      CWNUTKWIWQWNUTWTUFWFWIXAGULEWIULEVBGWHULUNUOVCUPUPUQVSWDVDBVEKWOVEKZWKBWO
      VECUFXDWOUTKWKWOXDXDPVFWNWKWSIULEZWKULELVGIVHLULEXEVIVJLVGULIVKVLIAULUNUO
      VCUPUQVTPVMUOWGWJWLFWEVNGWHVNIAVNVOVPVQVR $.
      $( [8-Apr-2008] $)

    ${
      hhssba.2 $e |- H e. SH $.
      $( The base set of a subspace. $)
      hhssba $p |- H = ( BaseSet ` W ) $=
        ( cva csm cop cno eqid csh wcel css cfv hhsst ax-mp shssii hhshsslem1
        ) EFGHGZABRIZCAJKBRLMKDRABSCNOADPQ $.
        $( [10-Apr-2008] $)

      $( The vector subtraction operation on a subspace. $)
      hhssvs $p |- ( -h |` ( H X. H ) ) = ( -v ` W ) $=
        ( cnsb cfv cmv cxp cres cva csm cop cno cnv wcel css wceq eqid hhnv
        csh hhsst ax-mp hhssba hhvs sspm mp2an eqcomi ) BEFZGAAHIZJKLMLZNOBUJPF
        ZOZUHUIQUJUJRZSATOULDUJABUMCUAUBUJUKUHGBAABCDUCUJUMUDUHRUKRUEUFUG $.
        $( [10-Apr-2008] $)

      $( Mapping of the vector subtraction operation on a subspace. $)
      hhssvsf $p |- ( -h |` ( H X. H ) ) : ( H X. H ) --> H $=
        ( cnv wcel cxp cmv cres wf hhssnv hhssba hhssvs nvmf ax-mp ) BEFAAGZAHP
        IZJABCDKBQAABCDLABCDMNO $.
        $( [10-Apr-2008] $)

      $( Inner product space property of a subspace. $)
      hhssph $p |- W e. CPreHil $=
        ( cva csm cop cno cphl wcel css cfv eqid hhph csh hhsst ax-mp sspph
        mp2an ) EFGHGZIJBTKLZJZBIJTTMZNAOJUBDTABUCCPQTUABUAMRS $.
        $( [10-Apr-2008] $)
    $}

    ${
      hhssims.2 $e |- H e. SH $.
      hhssims.3 $e |- D = ( ( normh o. -h ) |` ( H X. H ) ) $.
      $( Induced metric of a subspace. $)
      hhssims $p |- D = ( IndMet ` W ) $=
        ( cno cmv ccom cxp cres cims cfv resco crn wss wceq wf hhssvsf frn
        ax-mp cores eqtr4i cnv wcel hhssnv hhssvs hhssnm eqid imsval 3eqtr4i )
        GHIBBJZKZGBKZHULKZIZACLMZUMGUOIZUPGHULNUOOBPZUPURQULBUORUSBCDESULBUOTUA
        GUOBUBUAUCFCUDUEUQUPQBCDEUFUQCUOUNBCDEUGBCDUHUQUIUJUAUK $.
        $( [10-Apr-2008] $)
    $}
  $}

  ${
    hhssims2.1 $e |- W =
   <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. , ( normh |` H ) >. $.
    hhssims2.3 $e |- D = ( IndMet ` W ) $.
    ${
      hhssims2.2 $e |- H e. SH $.
      $( Induced metric of a subspace. $)
      hhssims2 $p |- D = ( ( normh o. -h ) |` ( H X. H ) ) $=
        ( cims cfv cno cmv ccom cxp cres eqid hhssims eqtr4i ) ACGHIJKBBLMZEQBC
        DFQNOP $.
        $( [10-Apr-2008] $)

      $( Induced metric of a subspace. $)
      hhssmet $p |- D e. Met $=
        ( cnv wcel cme hhssnv imsmet ax-mp ) CGHAIHBCDFJACEKL $.
        $( [10-Apr-2008] $)

      $( The base set for the metric of a subspace. $)
      hhssmetba $p |- H = dom dom D $=
        ( hhssba hhssnv imsbai ) ACBBCDFGEBCDFHI $.
        $( [10-Apr-2008] $)

      $( Value of the distance function of the metric space of a subspace. $)
      hhssmetdval $p |- ( ( A e. H /\ B e. H )
         -> ( A D B ) = ( normh ` ( A -h B ) ) ) $=
        ( wcel wa co cmv cxp cres cno cfv cnv wceq hhssnv hhssba hhssvs hhssnm
        imsdval mp3an1 oprvres fveq2d csh shsubcl fvres syl 3eqtrd ) ADIZBDIZJZ
        ABCKZABLDDMNZKZODNZPZABLKZURPZUTOPZEQIULUMUOUSRDEFHSABCEUPURDDEFHTDEFHU
        ADEFUBGUCUDUNUQUTURABDDLUEUFUNUTDIZVAVBRDUGIULUMVCHABDUHUDUTDOUIUJUK $.
        $( [10-Apr-2008] $)
    $}

    ${
      $d f j k x y D $.  $d f j k x y H $.
      hhsscms.3 $e |- H e. CH $.
      $( The induced metric of a closed subspace is complete. $)
      hhsscms $p |- D e. CMet $=
        ( vx vf vy vj vk chshii hhssmetba hhssmet cv cca cfv wcel cn wf wa
        chli wbr wrex clm simpr cc0 clt cle cmv co cno wi wral cr cme wb c1 1z
        nnuz iscauf mpan ffvelrn adantr adantlr hhssmetdval chil wceq normsub
        cheli syl2an eqtrd breq1d syl11anc imbi2d ralbidva rexbidva ralbidv
        bitrd biimpac ccau hcau2 biimpri wss chssii fss mpan2 sylan simpl cch
        chcompl mp3an1 lmbrf2 ancoms simplr rexbidv visset hlimconvi syl5bir
        reximdva sylc iscms2i ) GAHBABCDEBFLZMZABCDEXCNZHOZAPQRZSBXFTZUAZXHXFGO
        ZUBUCZGBUDZXFXJAUEQUCZGBUDXGXHUFZXIXHUGIOZUHUCZJOZKOZUIUCZXRXFQZXQXFQZU
        JUKULQZXOUHUCZUMZKSUNZJSUDZUMZIUOUNZXLXNXHXGYHXHXGXPXSYAXTAUKZXOUHUCZUM
        ZKSUNZJSUDZUMZIUOUNZYHAUPRZXHXGYOUQXEIAJKXFURBSXDUSUTVAVBXHYNYGIUOXHYMY
        FXPXHYLYEJSXHXQSRZUAZYKYDKSYRXRSRZUAZYJYCXSYTYABRZXTBRZYJYCUQYRUUAYSSBX
        QXFVCVDXHYSUUBYQSBXRXFVCZVEUUAUUBUAZYIYBXOUHUUDYIYAXTUJUKULQZYBYAXTABCD
        EXCVFYAVGRXTVGRUUEYBVHUUAUUBYAXTVIYABFVJXTBFVJVKVLVMVNVOVPVQVOVRVSVTXHY
        HUAXFWARZXHXLSVGXFTZYHUUFXHUUFUUGYHUAIJKXFWBWCXHBVGWDUUGBFWESBVGXFWFWGW
        HXHYHWIBWJRUUFXHXLFGXFBWKWLVNVNXHXKXMGBXHXJBRZUAZXMXPXSXTXJUJUKULQZXOUH
        UCZUMZKSUNZJSUDZUMZIUOUNZXKUUIXMXPXSXTXJAUKZXOUHUCZUMZKSUNZJSUDZUMZIUOU
        NZUUPUUHXHXMUVCUQZYPUUHXHUVDXEIAXJJKXFURBSXDUSUTWMWLWNUUIUVBUUOIUOUUIUV
        AUUNXPUUIUUTUUMJSUUIUUSUULKSUUIYSUAZUURUUKXSUVEUUQUUJXOUHUVEUUBUUHUUQUU
        JVHXHYSUUBUUHUUCVEXHUUHYSWOXTXJABCDEXCVFVNVMVOVPWPVOVRVSIJKXJXFHWQGWQWR
        WSWTXAXB $.
        $( [10-Apr-2008] $)
    $}
  $}

  ${
    hhssbn.1 $e |- W = <. <. ( +h |` ( H X. H ) ) , ( .h |` ( CC X. H ) ) >. ,
               ( normh |` H ) >. $.
    hhssbn.2 $e |- H e. CH $.
    $( Banach space property of a closed subspace. $)
    hhssbn $p |- W e. CBan $=
      ( cbn wcel cnv cims cfv cms eqid isbn chshii hhssnv hhsscms mpbir2an ) BE
      FBGFBHIZJFQBQKZLABCADMNQABCRDOP $.
      $( [10-Apr-2008] $)

    $( Hilbert space property of a closed subspace. $)
    hhsshl $p |- W e. CHil $=
      ( chl wcel cbn cphl ishl hhssbn chshii hhssph mpbir2an ) BEFBGFBHFBIABCDJ
      ABCADKLM $.
      $( [10-Apr-2008] $)
  $}

  ${
    $d x y z w H $.  $d x y z w A $.  $d x y B $.
    $( Value of orthogonal complement of a subset of Hilbert space. $)
    ocval $p |- ( H C_ ~H -> ( _|_ ` H ) =
        { x e. ~H | A. y e. H ( x .ih y ) = 0 } ) $=
      ( vz vw chil wss cpw wcel cort cfv cv csp co cc0 wceq wral crab ax-hilex
      elpw2 raleq rabbidv wa copab df-oc anbi1i opabbii eqtr4i rabex fvopab4
      sylbir ) CFGCFHZICJKALBLMNOPZBCQZAFRZPCFSTDECUMBDLZQZAFRZUOULJUPCPUQUNAFU
      MBUPCUAUBJUPFGZELURPZUCZDEUDUPULIZUTUCZDEUDDEABUEVCVADEVBUSUTUPFSTUFUGUHU
      NAFSUIUJUK $.
      $( [7-Aug-2000] $)

    $( Membership in orthogonal complement of H subset. $)
    ocel $p |- ( H C_ ~H -> ( A e. ( _|_ ` H ) <-> ( A e. ~H /\
                  A. x e. H ( A .ih x ) = 0 ) ) ) $=
      ( vy chil wss cort cfv wcel cv csp co cc0 wceq wral crab wa ocval eleq2d
      opreq1 eqeq1d ralbidv elrab syl6bb ) CEFZBCGHZIBDJZAJZKLZMNZACOZDEPZIBEIB
      UHKLZMNZACOZQUEUFULBDACRSUKUODBEUGBNZUJUNACUPUIUMMUGBUHKTUAUBUCUD $.
      $( [7-Aug-2000] $)

    $( Membership in orthogonal complement of H subspace. $)
    shocel $p |- ( H e. SH -> ( A e. ( _|_ ` H ) <-> ( A e. ~H /\
                  A. x e. H ( A .ih x ) = 0 ) ) ) $=
      ( csh wcel chil wss cort cfv cv csp co cc0 wceq wral wa wb shss ocel syl
      ) CDECFGBCHIEBFEBAJKLMNACOPQCRABCST $.
      $( [9-Oct-1999] $)

    $( The orthogonal complement of a subspace is a subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    ocsh $p |- ( A C_ ~H -> ( _|_ ` A ) e. SH ) $=
      ( vx vy vz cort cfv csh wcel chil wss c0v wa cv cva co wral csm cc sh
      csp cc0 wceq crab ssrab2 ocval sseq1d mpbiri ssel hi01 syl6 r19.21aiv
      ax-hv0cl jctil ocel mpbird jca wi caddc ax-his2 3expa opreq12 0cn
      addid1i syl6eq sylan9eq ex ancoms ssel2 sylan an1rs ralimdva imdistand
      hvaddcl anim1i anbi12d an4 r19.26 anbi2i bitr4i syl6bb 3imtr4d exp3a
      r19.21adv cmul opreq2 eqeq1d mul01 syl5cbir ad2antrl wb w3a ax-his3
      sylibrd hvmulcl anbi2d anass syl6bbr sylanbrc ) AEFZGHWSIJZKWSHZLBMZCMZNO
      ZWSHZCWSPZBWSPZXBXCQOZWSHZCWSPZBRPZLAIJZBCWSSXLWTXAXLWTXBXCTOUAUBCAPZBIUC
      ZIJXMBIUDXLWSXNIBCAUEUFUGXLXAKIHZKXCTOUAUBZCAPZLXLXQXOXLXPCAXLXCAHXCIHZXP
      AIXCUHXCUIUJUKULUMCKAUNUOUPXLXGXKXLXFBWSXLXBWSHZXECWSXLXSXCWSHZXEXLXBIHZX
      RLZXBDMZTOZUAUBZXCYCTOZUAUBZLZDAPZLZXDIHZXDYCTOZUAUBZDAPZLZXSXTLZXEXLYJYB
      YNLYOXLYBYIYNXLYBYIYNUQXLYBLYHYMDAXLYCAHZYBYHYMUQZYCIHZYBYRXLYQLZYBYSYRYB
      YSLZYHYMUUAYHYLYDYFUROZUAYAXRYSYLUUBUBXBXCYCUSUTYHUUBUAUAUROUAYDUAYFUAURV
      AUAVBVCVDVEVFVGAIYCVHZVIVJVKVFVLYBYKYNXBXCVMVNUJXLYPYAYEDAPZLZXRYGDAPZLZL
      ZYJXLXSUUEXTUUGDXBAUNDXCAUNZVOUUHYBUUDUUFLZLYJYAUUDXRUUFVPYIUUJYBYEYGDAVQ
      VRVSVTDXDAUNWAWBWCUKXLXJBRXLXBRHZXICWSXLUUKXTXIXLUUKXRLZUUFLZXHIHZXHYCTOZ
      UAUBZDAPZLZUUKXTLZXIXLUUMUULUUQLUURXLUULUUFUUQXLUULUUFUUQUQXLUULLYGUUPDAX
      LYQUULYGUUPUQZYSUULUUTYTYSUULLYGXBYFWDOZUAUBZUUPUUKYGUVBUQYSXRYGUVBXBUAWD
      OZUAUBUUKYGUVAUVCUAYFUAXBWDWEWFXBWGWHWIUULYSUUPUVBWJZUUKXRYSUVDUUKXRYSWKU
      UOUVAUAXBXCYCWLWFUTVGWMUUCVIVJVKVFVLUULUUNUUQXBXCWNVNUJXLUUSUUKUUGLUUMXLX
      TUUGUUKUUIWOUUKXRUUFWPWQDXHAUNWAWBWCUKUPWR $.
      $( [7-Aug-2000] $)

    $( The orthogonal complement of a subspace is a subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    shocsh $p |- ( A e. SH -> ( _|_ ` A ) e. SH ) $=
      ( csh wcel chil wss cort cfv shss ocsh syl ) ABCADEAFGBCAHAIJ $.
      $( [10-Oct-1999] $)

    $( An orthogonal complement is a subset of Hilbert space. $)
    ocss $p |- ( A C_ ~H -> ( _|_ ` A ) C_ ~H ) $=
      ( chil wss cort cfv csh wcel ocsh shss syl ) ABCADEZFGKBCAHKIJ $.
      $( [9-Aug-2000] $)

    $( An orthogonal complement is a subset of Hilbert space. $)
    shocss $p |- ( A e. SH -> ( _|_ ` A ) C_ ~H ) $=
      ( csh wcel chil wss cort cfv shss ocss syl ) ABCADEAFGDEAHAIJ $.
      $( [11-Oct-1999] $)

    $( Contraposition law for orthogonal complement. $)
    occon $p |- ( ( A C_ ~H /\ B C_ ~H ) ->
                 ( A C_ B -> ( _|_ ` B ) C_ ( _|_ ` A ) ) ) $=
      ( vx vy chil wss wa cort cfv cv csp co cc0 wceq wral crab wi wcel wal
      ssel imim1d alimdv df-ral 3imtr4g a1d r19.21aiv ss2rab sylibr adantl
      ocval ad2antlr ad2antrr 3sstr4d ex ) AEFZBEFZGZABFZBHIZAHIZFUQURGCJZDJZKL
      MNZDBOZCEPZVCDAOZCEPZUSUTURVEVGFZUQURVDVFQZCEOVHURVICEURVIVAERURVBBRZVCQZ
      DSVBARZVCQZDSVDVFURVKVMDURVLVJVCABVBTUAUBVCDBUCVCDAUCUDUEUFVDVFCEUGUHUIUP
      USVENUOURCDBUJUKUOUTVGNUPURCDAUJULUMUN $.
      $( [8-Aug-2000] $)

    $( Double contraposition for orthogonal complement. $)
    occon2 $p |- ( ( A C_ ~H /\ B C_ ~H ) ->
              ( A C_ B -> ( _|_ ` ( _|_ ` A ) ) C_ ( _|_ ` ( _|_ ` B ) ) ) ) $=
      ( chil wss wa cort cfv ocss anim12i ancoms occon sylsyld ) ACDZBCDZEBFGZC
      DZAFGZCDZEZABDOQDQFGOFGDNMSNPMRBHAHIJABKOQKL $.
      $( [22-Jul-2001] $)

    ${
      occon2.1 $e |- A C_ ~H $.
      occon2.2 $e |- B C_ ~H $.
      $( Double contraposition for orthogonal complement. $)
      occon2i $p |- ( A C_ B ->
                   ( _|_ ` ( _|_ ` A ) ) C_ ( _|_ ` ( _|_ ` B ) ) ) $=
        ( chil wss cort cfv wi occon2 mp2an ) AEFBEFABFAGHGHBGHGHFICDABJK $.
        $( [9-Aug-2000] $)
    $}

    $( The zero vector belongs to an orthogonal complement of a Hilbert
       subspace. $)
    oc0 $p |- ( H e. SH -> 0h e. ( _|_ ` H ) ) $=
      ( csh wcel cort cfv c0v shocsh sh0 syl ) ABCADEZBCFJCAGJHI $.
      $( [11-Oct-1999] $)

    $( Members of a subset and its complement are orthogonal. $)
    ocorth $p |- ( H C_ ~H -> ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
                 ( A .ih B ) = 0 ) ) $=
      ( vx chil wss wcel cort cfv wa csp co cc0 wceq cv wral ocel simplbda
      adantl wi opreq2 eqeq1d rcla4v ad2antlr wb orthcom ssel2 ocss sseld imp
      syl2an sylibrd mpd anandis ex ) CEFZACGZBCHIZGZJABKLMNZUPUQUSUTUPUQJZUPUS
      JZJZBDOZKLZMNZDCPZUTVBVGVAUPUSBEGZVGDBCQRSVCVGBAKLZMNZUTUQVGVJTUPVBVFVJDA
      CVDANVEVIMVDABKUAUBUCUDAEGVHUTVJUEVAVBABUFCEAUGUPUSVHUPUREBCUHUIUJUKULUMU
      NUO $.
      $( [9-Aug-2000] $)

    $( Members of a subspace and its complement are orthogonal. $)
    shocorth $p |- ( H e. SH -> ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
                 ( A .ih B ) = 0 ) ) $=
      ( csh wcel chil wss cort cfv wa csp co cc0 wceq wi shss ocorth syl ) CDEC
      FGACEBCHIEJABKLMNOCPABCQR $.
      $( [10-Oct-1999] $)

    $( Inclusion in complement of complement.  Part of Proposition 1 of
       [Kalmbach] p. 65. $)
    ococss $p |- ( A C_ ~H -> A C_ ( _|_ ` ( _|_ ` A ) ) ) $=
      ( vy vx chil wss cort cfv cv wcel csp co cc0 wceq wral wa ssel ocorth
      exp3a r19.21adv jcad wb ocss ocel syl sylibrd ssrdv ) ADEZBAAFGZFGZUGBHZA
      IZUJDIZUJCHZJKLMZCUHNZOZUJUIIZUGUKULUOADUJPUGUKUNCUHUGUKUMUHIUNUJUMAQRSTU
      GUHDEUQUPUAAUBCUJUHUCUDUEUF $.
      $( [9-Aug-2000] $)

    $( Inclusion in complement of complement.  Part of Proposition 1 of
       [Kalmbach] p. 65. $)
    shococss $p |- ( A e. SH -> A C_ ( _|_ ` ( _|_ ` A ) ) ) $=
      ( csh wcel chil wss cort cfv shss ococss syl ) ABCADEAAFGFGEAHAIJ $.
      $( [10-Oct-1999] $)

    $( Members of orthogonal subspaces are orthogonal. $)
    shorth $p |- ( H e. SH -> ( G C_ ( _|_ ` H ) ->
        ( ( A e. G /\ B e. H ) -> ( A .ih B ) = 0 ) ) ) $=
      ( csh wcel cort cfv wss wa csp co cc0 wceq shocorth imp chil wb shss
      sseld shocss anim12d orthcom syl mpbid ssel anim1d ancom sylib sylan2
      exp32 ) DEFZCDGHZIZACFZBDFZJZABKLMNZULUPAUMFZJZURUNUQJZULUTJZBAKLMNZURULU
      TVCBADOPVBBQFZAQFZJZVCURRULUTVFULUPVDUSVEULDQBDSTULUMQADUATUBPBAUCUDUEVAU
      SUPJZUTUNUQVGUNUOUSUPCUMAUFUGPUSUPUHUIUJUK $.
      $( [17-Oct-1999] $)

    $( Intersection of a Hilbert subspace and its complement.  Part of
       Proposition 1 of [Kalmbach] p. 65. $)
    ocin $p |- ( A e. SH -> ( A i^i ( _|_ ` A ) ) = 0H ) $=
      ( vx vy csh wcel cort cfv cin c0h cv wa c0v wceq chil csp co cc0 wral wi
      shocel opreq2 eqeq1d rcla4cv his6 biimpd sylan9r syl6bi com23 imp3a
      eleq1 anbi12d sh0 oc0 jca syl5cbir impbid elin elch0 3bitr4g eqrdv ) ADEZ
      BAAFGZHZIVABJZAEZVDVBEZKZVDLMZVDVCEVDIEVAVGVHVAVEVFVHVAVFVEVHVAVFVDNEZVDC
      JZOPZQMZCARZKVEVHSCVDATVMVEVDVDOPZQMZVIVHVLVOCVDAVJVDMVKVNQVJVDVDOUAUBUCV
      IVOVHVDUDUEUFUGUHUIVHVGLAEZLVBEZKVAVHVEVPVFVQVDLAUJVDLVBUJUKVAVPVQAULAUMU
      NUOUPVDAVBUQVDURUSUT $.
      $( [11-Oct-1999] $)
  $}

  $( A nonzero vector in the complement of a subspace does not belong to the
     subspace. $)
  ocnel $p |- ( ( H e. SH /\ A e. ( _|_ ` H ) /\ A =/= 0h ) -> -. A e. H ) $=
    ( csh wcel cort cfv c0v wne wn wa c0h wceq wi cin ocin eleq2d biimpd elin
    syl5ibr exp3a com23 imp elch0 syl6ib necon3ad 3impia ) BCDZABEFZDZAGHABDZIU
    GUIJZUJAGUKUJAKDZAGLUGUIUJULMUGUJUIULUGUJUIULUGABUHNZDZULUJUIJUGUNULUGUMKAB
    OPQABUHRSTUAUBAUCUDUEUF $.
    $( [10-Apr-2006] $)

  ${
    $d x y A $.
    chocval.1 $e |- A e. CH $.
    $( Value of the orthogonal complement of a Hilbert lattice element.  The
       orthogonal complement of ` A ` is the set of vectors that are orthogonal
       to all vectors in ` A ` . $)
    chocvali $p |- ( _|_ ` A ) = { x e. ~H | A. y e. A ( x .ih y ) = 0 } $=
      ( chil wss cort cfv cv csp co cc0 wceq wral crab chssii ocval ax-mp ) CEF
      CGHAIBIJKLMBCNAEOMCDPABCQR $.
      $( [8-Aug-2004] $)
  $}

  ${
    chocuni.1 $e |- H e. CH $.
    $( Lemma for uniqueness part of Projection Theorem.  Theorem 3.7(i) of
       [Beran] p. 102 (uniqueness part). $)
    chocunii $p |- ( ( ( A e. H /\ B e. ( _|_ ` H ) ) /\
                    ( C e. H /\ D e. ( _|_ ` H ) ) ) ->
            ( ( R = ( A +h B ) /\ R = ( C +h D ) ) -> ( A = C /\ B = D ) ) ) $=
      ( wcel cort cfv wa cva co cmv wceq chil wb c0v simpl anim1i hvsub4
      syldan hvsubid ad2antrr opreq1d hvsubcl hvaddid2 syl adantll 3eqtrd
      adantrl simpr anim2i syl11anc ad2antll opreq2d ax-hvaddid ancoms adantrr
      adantlr eqeq12d cheli csh chshii shocsh ax-mp sheli anim12i syl2an wi
      shsubclOLD a1d ad2ant2r eleq1 syl5cbi ad2ant2l jcad hvsubeq0 eqcom
      syl6bb cin c0h ocin eleq2i elin elch0 3bitr3i syl5bb sylibd eleq1a
      sylbid eqtr2 syl5 ) AFHZBFIJZHZKZCFHZDWOHZKZKZABLMZADLMZNMZCDLMZXCNMZOZAC
      OZBDOZKZEXBOEXEOKZXAXGBDNMZCANMZOZXJAPHZBPHZKZCPHZDPHZKZXGXNQWQWTXQXTKXDX
      LXFXMXQXSXDXLOXRXQXSKZXDAANMZXLLMZRXLLMZXLXQXSXOXSKZXDYCOXQXOXSXOXPSTABAD
      UAUBYAYBRXLLXOYBROXPXSAUCUDUEXPXSYDXLOZXOXPXSKXLPHYFBDUFXLUGUHUIUJUKXOXTX
      FXMOXPXOXTKZXFXMDDNMZLMZXMRLMZXMYGXTYEXFYIOXOXTULXTXSXOXRXSULUMCDADUAUNYG
      YHRXMLXSYHROXOXRDUCUOUPXOXRYJXMOZXSXRXOYKXRXOKXMPHYKCAUFXMUQUHURUSUJUTVAW
      NXOWPXPAFGVBBWOFVCHZWOVCHZFGVDZFVEVFZVGVHZWRXRWSXSCFGVBDWOYOVGVHZVIXAXNXH
      XIXAXNXMFHZXMWOHZKZXHXAXNYRYSWNWRXNYRVJWPWSWNWRKZYRXNWRWNYRYLWRWNKYRVJYNC
      AFVKVFURZVLVMWPWSXNYSVJWNWRXNXLWOHZYSWPWSKZXLXMWOVNYMUUDUUCVJYOBDWOVKVFZV
      OVPVQXAXMROZXHYTXAUUFCAOZXHXQXTUUFUUGQZWQWTXOXRUUHXPXSXRXOUUHCAVRURVMYPYQ
      VICAVSVTXMFWOWAZHXMWBHYTUUFUUIWBXMYLUUIWBOYNFWCVFZWDXMFWOWEXMWFWGWHWIXAXN
      XLFHZUUCKZXIXAXNUUKUUCWNWRXNUUKVJZWPWSUUAYRUUMUUBXMFXLWJUHVMWPWSXNUUCVJWN
      WRUUDUUCXNUUEVLVPVQXAXLROZXIUULXQXTUUNXIQZWQWTXPXSUUOXOXRBDVRVPYPYQVIXLUU
      IHXLWBHUULUUNUUIWBXLUUJWDXLFWOWEXLWFWGWHWIVQWKXKXBXEXCNEXBXEWLUEWM $.
      $( [23-Oct-1999] $)
  $}

  ${
    occllem1.1 $e |- A e. ~H $.
    occllem1.2 $e |- B e. ~H $.
    occllem1.3 $e |- S e. ~H $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem1 $p |- ( abs ` ( ( B .ih S ) - ( A .ih S ) ) ) <_
                    ( ( normh ` ( B -h A ) ) x. ( normh ` S ) ) $=
      ( csp co cmin cabs cfv cmv cno cmul cle c1 cneg caddc csm cva chil wcel
      wceq ax1cn negcli hvmulcli ax-his2 mp3an cc ax-his3 opreq2i eqtr2i hicli
      mulm1i negsubi hvsubvali opreq1i 3eqtr4i fveq2i hvsubcli bcsiHIL eqbrtri
      ) BCGHZACGHZIHZJKBALHZCGHZJKVFMKCMKNHOVEVGJVCPQZVDNHZRHZBVHASHZTHZCGHZVEV
      GVMVCVKCGHZRHZVJBUAUBVKUAUBCUAUBZVMVOUCEVHAPUDUEZDUFFBVKCUGUHVNVIVCRVHUIU
      BAUAUBVPVNVIUCVQDFVHACUJUHUKULVJVCVDQZRHVEVIVRVCRVDACDFUMZUNUKVCVDBCEFUMV
      SUOULVFVLCGBAEDUPUQURUSVFCBAEDUTFVAVB $.
      $( [12-Oct-1999] $)
  $}

  ${
    occllem2.1 $e |- S e. ~H $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem2 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( abs ` ( ( B .ih S ) - ( A .ih S ) ) ) <_
                  ( ( normh ` ( B -h A ) ) x. ( normh ` S ) ) ) $=
      ( chil wcel csp co cmin cabs cfv cmv cno cmul cle wbr c0v cif wceq
      opreq1 opreq2d fveq2d opreq2 opreq1d breq12d ax-hv0cl elimel occllem1
      dedth2h ) AEFZBEFZBCGHZACGHZIHZJKZBALHZMKZCMKZNHZOPULUJAQRZCGHZIHZJKZBUTL
      HZMKZURNHZOPUKBQRZCGHZVAIHZJKZVGUTLHZMKZURNHZOPABQQAUTSZUOVCUSVFOVNUNVBJV
      NUMVAULIAUTCGTUAUBVNUQVEURNVNUPVDMAUTBLUCUBUDUEBVGSZVCVJVFVMOVOVBVIJVOULV
      HVAIBVGCGTUDUBVOVEVLURNVOVDVKMBVGUTLTUBUDUEUTVGCAQEUFUGBQEUFUGDUHUI $.
      $( [13-Oct-1999] $)
  $}

  ${
    $d x y z F $.  $d x y z S $.  $d z G $.  $d x y D $.
    occllem3.1 $e |- G =
                    { <. x , y >. | ( x e. NN /\ y = ( ( F ` x ) .ih S ) ) } $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem3 $p |- ( D e. NN -> ( G ` D ) = ( ( F ` D ) .ih S ) ) $=
      ( cv cfv csp co cn wceq fveq2 opreq1d oprex fvopab4 ) ABCAHZEIZDJKCEIZDJK
      LFRCMSTDJRCENOGTDJPQ $.
      $( [13-Oct-1999] $)

    ${
      occllem4.2 $e |- S e. ~H $.
      $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
         3.12 of [Beran] p. 107. $)
      occllem4 $p |- ( F : NN --> ~H -> G : NN --> CC ) $=
        ( cn chil wf cv cfv csp co cc wcel wral wa ffvelrn jctir hicl syl
        r19.21aiva fopab2 sylib ) HIDJZAKZDLZCMNZOPZAHQHOEJUFUJAHUFUGHPRZUHIPZC
        IPZRUJUKULUMHIUGDSGTUHCUAUBUCABHOUIEFUDUE $.
        $( [12-Oct-1999] $)
    $}

    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem5 $p |- ( A. z e. NN ( ( F ` z ) .ih S ) = 0 -> G ~~> 0 ) $=
      ( cv cfv csp co cc0 wceq cn wral csn cxp cli wcel wa copab wb fveq2
      opreq1d eqeq1d rcla4v eqeq2 syl6com pm5.32d opabbidv fconstopab 3eqtr4g
      c1 cuz nnuz xpeq1i 1z climuz0i eqbrtri syl6eqbr ) CHZEIZDJKZLMZCNOZFNLPZQ
      ZLRVEAHZNSZBHZVHEIZDJKZMZTZABUAVIVJLMZTZABUAFVGVEVNVPABVEVIVMVOVIVEVLLMZV
      MVOUBVDVQCVHNVAVHMZVCVLLVRVBVKDJVAVHEUCUDUEUFVLLVJUGUHUIUJGABNLUKULVGUMUN
      IZVFQLRNVSVFUOUPUMUQURUSUT $.
      $( [6-Jul-2006] $) $( [12-Oct-1999] $)
  $}

  ${
    $d z w v u A $.  $d x y z w v u F $.  $d w u v G $.  $d x y z w v u S $.
    occllem6.1 $e |- G =
                    { <. x , y >. | ( x e. NN /\ y = ( ( F ` x ) .ih S ) ) } $.
    occllem6.2 $e |- A e. ~H $.
    occllem6.3 $e |- S e. ~H $.
    occllem6.4 $e |- F e. _V $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem6 $p |- ( S =/= 0h -> ( F ~~>v A -> G ~~> ( A .ih S ) ) ) $=
      ( vv vu vw vz c0v wne chli wbr csp co cc wcel cv cfv cn wral wa cc0 clt
      cle cmin cabs wi wrex cr cli chil wf cmv cno ffvelrn occllem4 sylan
      r19.21aiva hicli jctil adantr a1i adantrd cdiv normcli redivcl mp3an2
      gt0ne0i sylan2 adantlr divgt0 mpanr1 jca expcom imim1d cmul wb jctir
      hvsubcl normcl 3syl adantll simplrl simplll ltmuldivOLD mp3anl2 syl21anc
      occllem2 syl occllem3 opreq1d fveq2d breq1d adantl mpbird lelttr 3expa
      subcl abscl remulcl mpand adantrr an1rs sylbird imim2d ralimdva reximdv
      ex a2d syld exp4a normgt0 ax-mp sylanb wceq breq2 imbi2d rexralbidv
      imbi12d rcla4cv imp3a syl5 r19.21adv expimpd jcad elisseti hlimi syl5ib
      cin c1 1z cuz nnuz eqimss2i nnssz nnex fopabex2 clm4i wss inss2 ssralv
      biimpar syl6 ) DOPZECQRZCDSTZUAUBZKUCZFUDZUAUBZKUEUFZUGZUHLUCZUIRZMUCUUNU
      JRZUUOUULUKTZULUDZUUSUIRZUMZKUEUFZMUEUNZUMZLUOUFZUGZFUULUPRZUUJUEUQEURZCU
      QUBZUGZUHNUCZUIRZUVAUUNEUDZCUSTZUTUDZUVOUIRZUMZKUEUFMUEUNZUMZNUOUFZUGZUVJ
      UUKUUJUWEUURUVIUUJUVNUURUWDUVNUURUMUUJUVLUURUVMUVLUUQUUMUVLUUPKUEUEUAFURU
      UNUEUBZUUPUVLUEUAUUNFVAABDEFGIVBVCZVDCDHIVEZVFVGVHVIUUJUVNUWDUVIUUJUVNUGZ
      UWDUVHLUOUWIUUSDUTUDZVJTZUOUBZUHUWKUIRZUGZUVAUVSUWKUIRZUMZKUEUFZMUEUNZUMZ
      UUSUOUBZUVHUMZUWDUUJUVLUWSUXAUMZUVMUHUWJUIRZUVLUXBUUJUXCUVLUGZUWSUWTUUTUV
      GUXDUWSUWTUUTUGZUWRUMUXEUVGUMUXDUXEUWNUWRUXCUXEUWNUMUVLUXEUXCUWNUXEUXCUGU
      WLUWMUWTUXCUWLUUTUWTUWJUHPZUWLUXCUWTUWJUOUBZUXFUWLDIVKZUUSUWJVLVMUWJUXHVN
      VOVPUXEUXGUXCUWMUXHUUSUWJVQVRVSVTVGWAUXDUXEUWRUVGUXDUXEUWRUVGUMUXDUXEUGZU
      WQUVFMUEUXIUWPUVEKUEUXIUWFUGZUWOUVDUVAUXJUWOUVSUWJWBTZUUSUIRZUVDUXJUVSUOU
      BZUWTUXCUXLUWOWCZUXDUWFUXMUXEUVLUWFUXMUXCUVLUWFUGZUVQUQUBZUVMUGUVRUQUBUXM
      UXOUXPUVMUEUQUUNEVAZHWDUVQCWEUVRWFWGZWHVPUXDUWTUUTUWFWIUXCUVLUXEUWFWJUXMU
      XGUWTUXCUXNUXHUVSUWJUUSWKWLWMUXDUWFUXEUXLUVDUMZUXDUWFUGZUWTUXSUUTUXTUWTUG
      UVCUXKUJRZUXLUVDUXTUYAUWTUVLUWFUYAUXCUXOUYAUVQDSTZUULUKTZULUDZUXKUJRZUXOU
      VMUXPUGUYEUXOUXPUVMUXQHVFCUVQDIWNWOUWFUYAUYEWCUVLUWFUVCUYDUXKUJUWFUVBUYCU
      LUWFUUOUYBUULUKABUUNDEFGWPWQWRWSWTXAWHVGUVCUOUBZUXKUOUBZUGZUWTUYAUXLUGUVD
      UMZUXTUYFUYGUWTUYIUVCUXKUUSXBXCUVLUWFUYHUXCUXOUYFUYGUXOUUPUUMUGUVBUAUBUYF
      UXOUUPUUMUWGUWHWDUUOUULXDUVBXEWGUXOUXMUXGUGUYGUXOUXMUXGUXRUXHWDUVSUWJXFWO
      VSWHVCXGXHXIXJXKXLXMXNXOXPXQDUQUBUUJUXCWCIDXRXSXTXHUWDUWLUWMUWRUWCUWMUWRU
      MNUWKUOUVOUWKYAZUVPUWMUWBUWRUVOUWKUHUIYBUYJUWAUWPMKUEUEUYJUVTUWOUVAUVOUWK
      UVSUIYBYCYDYEYFYGYHYIYJYKNMKCEJCUQHYLYMYNUURUVKUVIUUMUUPKUEUEYOZUFZUVKUVI
      WCUUQLUULMKFYPYPUEUEYQUEYPYRUDYSYTZUUAYQUYMUUAABUEAUCEUDDSTFUUBGUUCUUDUYK
      UEUUEUUQUYLUMUEUEUUFUUPKUYKUEUUGXSVOUUHUUI $.
      $( [17-Nov-2007] $) $( [12-Oct-1999] $)
  $}

  ${
    $d x y z F $.  $d x y z S $.
    occllem7.1 $e |- A e. ~H $.
    occllem7.2 $e |- S e. ~H $.
    occllem7.3 $e |- F e. _V $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem7 $p |- ( ( F ~~>v A /\
               A. x e. NN ( ( F ` x ) .ih S ) = 0 ) -> ( A .ih S ) = 0 ) $=
      ( vy vz chli wbr cv cfv csp co cc0 wceq cn wral wa wi c0v opreq2 chil
      wcel hi02 ax-mp syl6eq a1d wne copab cli eqid occllem6 occllem5 a1i
      anim12d oprex cr 0re elisseti climuni syl6 pm2.61ine ) DBJKZALDMCNOPQARSZ
      TZBCNOZPQZUACUBCUBQZVIVGVJVHBUBNOZPCUBBNUCBUDUEVKPQEBUFUGUHUICUBUJZVGHLZR
      UEILVMDMCNOQTHIUKZVHULKZVNPULKZTVIVLVEVOVFVPHIBCDVNVNUMZEFGUNVFVPUAVLHIAC
      DVNVQUOUPUQVHPVNBCNURPUSUTVAVBVCVD $.
      $( [6-Jul-2006] $) $( [12-Oct-1999] $)
  $}

  ${
    $d x y z F $.  $d x y z S $.
    occllem8.1 $e |- F e. _V $.
    $( Lemma for closure of complement of Hilbert subspace.  Part of Remark
       3.12 of [Beran] p. 107. $)
    occllem8 $p |- ( ( A e. ~H /\ S e. ~H ) -> ( ( F ~~>v A /\
             A. x e. NN ( ( F ` x ) .ih S ) = 0 ) -> ( A .ih S ) = 0 ) ) $=
      ( chil wcel chli wbr cv cfv csp co cc0 wceq cn wral wa wi c0v cif breq2
      anbi1d opreq1 eqeq1d imbi12d opreq2 ralbidv anbi2d ax-hv0cl elimel
      occllem7 dedth2h ) BFGZCFGZDBHIZAJDKZCLMZNOZAPQZRZBCLMZNOZSDUNBTUAZHIZUTR
      ZVDCLMZNOZSVEUQUOCTUAZLMZNOZAPQZRZVDVILMZNOZSBCTTBVDOZVAVFVCVHVPUPVEUTBVD
      DHUBUCVPVBVGNBVDCLUDUEUFCVIOZVFVMVHVOVQUTVLVEVQUSVKAPVQURVJNCVIUQLUGUEUHU
      IVQVGVNNCVIVDLUGUEUFAVDVIDBTFUJUKCTFUJUKEULUM $.
      $( [13-Oct-1999] $)
  $}

  ${
    $d x y z f A $.
    occli.1 $e |- A C_ ~H $.
    $( Closure of complement of Hilbert subset.  Part of Remark 3.12 of [Beran]
       p. 107. $)
    occli $p |- ( _|_ ` A ) e. CH $=
      ( vf vx vy vz cort cfv cch wcel csh cn cv wf chli wbr wa wi wal
      closedsub chil wss ocsh ax-mp csp co cc0 wceq wral wb ocel visset
      hlimveci adantl occllem8 exp4b sseli syl5 com23 mpcom imp ralimdva
      ffvelrn simprbi syl r19.21aiva ralcom sylibr impcom sylanbrc gen2
      mpbir2an ) AGHZIJVMKJZLVMCMZNZVODMZOPZQZVQVMJZRZDSCSDCVMTAUAUBZVNBAUCUDWA
      CDVTVQUAJZVQEMZUEUFUGUHZEAUIZVSWBVTWCWFQUJBEVQAUKUDVRWCVPVQVOCULZDULUMZUN
      VRVPWFVRFMZVOHZWDUEUFUGUHZFLUIZEAUIZWFVPVRWLWEEAVRWDAJZWLWERZWCVRWNWORWHW
      CWNVRWOWCWDUAJZVRWORWNWCWPVRWLWEFVQWDVOWGUOUPAUAWDBUQURUSUTVAVBVPWKEAUIZF
      LUIWMVPWQFLVPWILJQWJVMJZWQLVMWIVOVCWRWJUAJZWQWBWRWSWQQUJBEWJAUKUDVDVEVFWK
      EFALVGVHURVIVJVKVL $.
      $( [7-Aug-2000] $)
  $}

  $( Closure of complement of Hilbert subset.  Part of Remark 3.12 of [Beran]
     p. 107. $)
  occl $p |- ( A C_ ~H -> ( _|_ ` A ) e. CH ) $=
    ( chil wss cort cfv cch wcel cif wceq fveq2 eleq1d sseq1 ssid elimhyp
    occli dedth ) ABCZADEZFGQABHZDEZFGABASIRTFASDJKSQSBCBBCABASBLBSBLBMNOP $.
    $( [8-Aug-2000] $)

  $( Closure of complement of Hilbert subspace.  Part of Remark 3.12 of [Beran]
     p. 107. $)
  shoccl $p |- ( A e. SH -> ( _|_ ` A ) e. CH ) $=
    ( csh wcel chil wss cort cfv cch shss occl syl ) ABCADEAFGHCAIAJK $.
    $( [13-Oct-1999] $)

  $( Closure of complement of Hilbert subspace.  Part of Remark 3.12 of [Beran]
     p. 107. $)
  choccl $p |- ( A e. CH -> ( _|_ ` A ) e. CH ) $=
    ( cch wcel csh cort cfv chsh shoccl syl ) ABCADCAEFBCAGAHI $.
    $( [22-Jul-2001] $)

  ${
    choccl.1 $e |- A e. CH $.
    $( Closure of ` CH ` orthocomplement. $)
    choccli $p |- ( _|_ ` A ) e. CH $=
      ( chssii occli ) AABCD $.
      $( [29-Jul-1999] $)
  $}

