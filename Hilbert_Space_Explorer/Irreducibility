$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Irreducibility
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d p q r x A $.
    irred.1 $e |- A e. CH $.
    $( Lemma for ~ irred . $)
    irredlem1 $p |- ( ( ( p e. Atoms /\ ( q e. CH /\ q (_ ( _|_ ` A ) ) )
     /\ ( ( r e. Atoms /\ r (_ A ) /\ r (_ ( p vH q ) ) ) ->
       ( p i^i ( _|_ ` r ) ) = 0H ) $=
      ( cv cat wcel cch cort cfv wss wa chj co cin c0h wceq wi sstr2 wb
      chsscon3t mpan2 biimpa atelch sylan syl5 wn wne atn0 df-ne sylib adantr
      ad2antrr simplr chlej1tOLD chocclt syl syl3an2 3com12 3expb imp adantllr
      adantlr sstrd chlejb2t ancoms sylanl1 an1rs adantrl sseqtrd ex chssoct
      biimpd syld mtod sylanr1 atnssm0 incom eqeq1i syl6bb ad2ant2r sylibd
      exp43 sylcom com4t imp3a imp43 ) DFZGHZCFZIHZWKAJKZLZMBFZGHZWOALZMZWOWIWK
      NOZLZWIWOJKZPZQRZWJWLWNWRWTXCSZSWNWRWJWLXDWNWRWKXALZWJWLXDSSZWNWMXALZXEWR
      WKWMXATWOIHZWQXGWPXHWQXGXHAIHWQXGUAEWOAUBUCUDWOUEZUFUGWPXEXFSWQWPXEWJWLXD
      WPXEMZWJWLMMWTWIXALZUHZXCXJWIIHZWLWTXLSWJXJXMWLMZMZWTXLXOWTMZXKWOQRZXJXQU
      HZXNWTWPXRXEWPWOQUIXRWOUJWOQUKULUMUNXPXKWOXALZXQXPXKXSXPXKMZWOXAWKNOZXAXT
      WOWSYAXOWTXKUOXOXKWSYALZWTWPXNXKYBXEWPXNMXKYBWPXMWLXKYBSZXMWPWLYCXMXAIHZW
      LYCWPWIXAWKUPWPXHYDXIWOUQURZUSUTVAVBVCVDVEXOYAXARZWTXKXJWLYFXMWPWLXEYFYDW
      LXEYFWPYDWLMXEYFWLYDXEYFUAWKXAVFVGUDYEVHVIVJUNVKVLXJXSXQSZXNWTWPYGXEWPXHY
      GXIXHXSXQWOVMVNURUMUNVOVPVLWIUEVQWPWJXLXCUAZXEWLYDWJYHWPYDWJMXLXAWIPZQRXC
      XAWIVRYIXBQXAWIVSVTWAYEUFWBWCWDUMWEWFWGWH $.
      $( [8-Jul-2006] $) $( [14-Jun-2006] $)

    $( Lemma for ~ irred . $)
    irredlem2 $p |- ( ( ( ( p e. Atoms /\ p (_ A ) /\ ( q e. CH /\
      q (_ ( _|_ ` A ) ) ) /\ ( ( r e. Atoms /\ r (_ A ) /\ r (_ ( p vH q ) ) )
                  -> ( ( _|_ ` r ) i^i ( p vH q ) ) = q ) $=
      ( cv cat wcel wss wa cch cort cfv chj co cin wceq chjcomt atelch sylan
      ad2ant2r adantr ineq2d w3a ccm wbr fh2t chocclt syl id 3anim123i 3com13
      3expa adantllr adantlrr adantrr lecmt simpll ad2antrl sstr wb chsscon3t
      mpan2 biimpa sylan2 adantll syl3anc an1s ancom2s wi syl3an2 ex cmcm2t
      sylibrd mpd sylanl2 ancom1s an4s jca sylanc c0h sseqin2 sylib irredlem1
      incom syl5eq opreq12d chj0t ad2antlr eqtrd 3eqtrd ) DFZGHZWLAIZJZCFZKHZWP
      ALMZIZJZJZBFZGHZXBAIZJZXBWLWPNOZIZJZJZXBLMZXFPXJWPWLNOZPZXJWPPZXJWLPZNOZW
      PXIXFXKXJXAXFXKQZXHWMWQXPWNWSWLKHZWQXPWMWLWPRWLSZTUAUBUCXJKHZWQXQUDZWPXJU
      EUFZWPWLUEUFZJXLXOQXIXJWPWLUGXAXEXTXGXAXCXTXDWOWQXCXTWSWMWQXCXTWNWMWQXCXT
      XCWQWMXTXCXSWQWQWMXQXCXBKHZXSXBSZXBUHUIZWQUJXRUKULUMUNUOUPUPXIYAYBWTXHYAW
      OWTXEYAXGWQXSWPXJIZYAWTXEJZWPXJUQWQWSXEURXCXSWTXDYEUSWSXEYFWQWSWRXJIZYFXE
      WPWRXJUTXCXDYHXCYCXDYHVAZYDYCAKHZYIEXBAVBVCUIVDVEVFZVGUPVFXAYBXHWMWQWNWSY
      BWQWMWNWSJZYBWQXQYLYBWMWQXQJZYLJWPWLLMZIZYBXQYLYOWQXQWSWNYOWSXQWNYOWSWRYN
      IZYOXQWNJWPWRYNUTXQWNYPXQYJWNYPVAEWLAVBVCVDVEVHVIVFYMYOYBVJYLYMYOWPYNUEUF
      ZYBYMYOYQWQXQYOYQWQYNKHYOYQXQWPYNUQWLUHVKUMVLWPWLVMVNUBVOXRVPVQVRUBVSVTXI
      XOWPWANOZWPXIXMWPXNWANWTXHXMWPQZWOWTXEYSXGYGYFYSYKWPXJWBWCUPVFXIWLXJPZWAX
      NWMWTXHYTWAQWNABCDEWDUNXJWLWEWFWGWTYRWPQZWOXHWQUUAWSWPWHUBWIWJWK $.
      $( [8-Jul-2006] $) $( [15-Jun-2006] $)

    ${
      irred.2 $e |- ( x e. CH -> A C_H x ) $.
      $( Lemma for ~ irred . $)
      irredlem3 $p |- ( ( ( ( p e. Atoms /\ p (_ A ) /\ ( q e. Atoms /\
        q (_ ( _|_ ` A ) ) ) /\ ( r e. Atoms /\ r (_ ( p vH q ) ) )
                    -> ( r (_ A -> r = p ) ) $=
        ( cv cat wcel wss wa cort cfv chj co wceq wi cch c0h cin irredlem2
        opreq2d pjoml2t atelch adantr chjclt sylan ad2ant2r id syl3an 3com12
        3expb eqtr3d ineq2d ccm wbr breq2 vtoclga anim12i fh1t mp3anl1 mpdan
        ancoms adantrr adantll 3eqtr3d sseqin2 biimp ad2antlr adantl csh chsh
        chshi orthin mpan2 syl imp incom syl5eq opreq12d ad2antrr chj0t exp44
        com34 sylanr1 imp32 ) EHZIJZWHBKZLZDHZIJZWLBMNKZLLCHZIJZWOWHWLOPZKZWOBK
        ZWOWHQZRZWKWLSJZWNWPWRXARRWMWKXBWNLZLZWPWSWRWTXDWPWSWRWTXDWPWSLZWRLZLZW
        OTOPZWHTOPZWOWHXGBWOUAZBWLUAZOPZBWHUAZXKOPZXHXIXGBWOWLOPZUAZBWQUAZXLXNX
        GXOWQBXGWOWOMNWQUAZOPZXOWQXGXRWLWOOBCDEFUBUCXDXEWRXSWQQZXEXDWRXTWOSJZWQ
        SJZWRXTXEXDWRWOWQUDWPYAWSWOUEZUFWIXBYBWJWNWHSJZXBYBWIWHWLUGWHUEZUHUIWRU
        JUKULUMUNUOXCXFXPXLQZWKXBXEYFWNWRXBWPYFWSWPXBYFYAXBYFWPYAXBLBWOUPUQZBWL
        UPUQZLZYFYAYGXBYHBAHZUPUQZYGAWOSYJWOBUPURGUSYKYHAWLSYJWLBUPURGUSZUTBSJZ
        YAXBYIYFFBWOWLVAVBVCYCUHVDVEUIVFXDXQXNQZXFWIXBYNWJWNYDXBYNWIYDXBLBWHUPU
        QZYHLZYNYDYOXBYHYKYOAWHSYJWHBUPURGUSYLUTYMYDXBYPYNFBWHWLVAVBVCYEUHUIUFV
        GXGXJWOXKTOXFXJWOQZXDWSYQWPWRWSYQWOBVHVIVJVKXCXKTQWKXFXCWLBUAZTXKXBWNYR
        TQZXBWLVLJZWNYSRZWLVMYTBVLJUUABFVNWLBVOVPVQVRBWLVSVTVJZWAXGXMWHXKTOWKXM
        WHQZXCXFWJUUCWIWJUUCWHBVHVIVKWBUUBWAVGXFXHWOQZXDWPUUDWSWRWPYAUUDYCWOWCV
        QWBVKWKXIWHQZXCXFWIUUEWJWIYDUUEYEWHWCVQUFWBVGWDWEWLUEWFWG $.
        $( [8-Jul-2006] $) $( [15-Jun-2006] $)

      $( Lemma for ~ irred . $)
      irredlem4 $p |- ( ( ( ( p e. Atoms /\ p (_ A ) /\ ( q e. Atoms /\
        q (_ ( _|_ ` A ) ) ) /\ ( r e. Atoms /\
              r (_ ( p vH q ) ) ) -> ( r = p \/ r = q ) ) $=
        ( cv cat wcel wss wa cort cfv chj co wo wceq ccm wbr cch atelch breq2
        vtoclga syl atord mpdan ad2antrl irredlem3 wi wb chjcomt syl2an sseq2d
        anbi2d ad2ant2r choccl cmcm3t mpan mpbid ex sylbird ococ sseq2i biimpr
        sylanr2 imp ancom1s orim12d mpd ) EHZIJZVKBKZLZDHZIJZVOBMNZKZLZLZCHZIJZ
        WAVKVOOPZKZLZLZWABKZWAVQKZQZWAVKRZWAVORZQWBWIVTWDWBBWASTZWIWBWAUAJWLWAU
        BBAHZSTZWLAWAUAWMWABSUCGUDUEBWAFUFUGUHWFWGWJWHWKABCDEFGUIVSVNWEWHWKUJZV
        SVNLWEWOVSVLVKVQMNZKZWEWOUJVMVSVLWQLLZWEWBWAVOVKOPZKZLZWOVPVLXAWEUKVRWQ
        VPVLLZWTWDWBXBWSWCWAVOUAJVKUAJWSWCRVPVLVOVKULVOUBVKUBUMUNUOUPWRXAWOAVQC
        EDBFUQWMUAJZWNVQWMSTZGBUAJXCWNXDUKFBWMURUSUTUIVAVBWQVMWPBVKBFVCVDVEVFVG
        VHVIVJ $.
        $( [8-Jul-2006] $) $( [15-Jun-2006] $)

      $( The Hilbert lattice is irreducible:  any element that commutes with
         all elements must be zero or one.  Theorem 14.8.4 of
         [BeltramettiCassinelli] p. 166. $)
      irred $p |- ( A = 0H \/ A = H~ ) $=
        ( vp vq vr c0h wceq cort cfv wo chil eqid wn wne wa ioran df-ne
        anbi12i bitr4 cv wss cat wrex hatomic choccl anim12i reeanv sylibr
        wcel chj co w3a superpos simpll simprl sstr cch wb atelch chsscon3t
        mpan2 syl biimpa sylan2 ancoms atn0 adantr sseq1 bicomd chssoct
        sylan9bbr an1rs ex necon3d mpd adantlr syldan adantrl syl3anc wi
        irredlem4 anassrs pm2.21nd com23 imp3a df-3an anbi1i bitr syl5ib
        r19.23adva an4s r19.23aivv sylbi a3i ax-mp fveq2 ococ choc0 3eqtr3g
        orim2i ) BHIZBJKZHIZLZXCBMIZLHHIZXFHNXFXHXFOZBHPZXDHPZQZXHOZXIXCOZXEOZQ
        XLXCXERXJXNXKXOBHSXDHSTUAXLEUBZBUCZFUBZXDUCZQZFUDUEEUDUEZXMXLXQEUDUEZXS
        FUDUEZQYAXJYBXKYCEBCUFFXDBCUGUFUHXQXSEFUDUDUIUJXTXMEFUDUDXPUDUKZXRUDUKZ
        QXTXMYDXQYEXSXMYDXQQZYEXSQZQZGUBZXPPZYIXRPZYIXPXRULUMUCZUNZGUDUEZXMYDYE
        XPXRPZYNYHGXPXRUOYDXQYGUPYFYEXSUQYFXSYOYEYFXSXRXPJKZUCZYOXSYFYQXSXDYPUC
        ZYQYFXRXDYPURYDXQYRYDXPUSUKZXQYRUTZXPVAZYSBUSUKYTCXPBVBVCVDVEVFVGYDYQYO
        XQYDYQQZXPHPZYOYDUUCYQXPVHVIUUBXPXRXPHUUBXPXRIZXPHIZYDUUDYQUUEYDUUDQYQU
        UEUUDYQXPYPUCZYDUUEUUDUUFYQXPXRYPVJVKYDYSUUFUUEUTUUAXPVLVDVMVEVNVOVPVQV
        RVSVTWAYHYMXMGUDYHYIUDUKZQZYIXPIZYIXRIZLZOZYLQZXMYMUUHUULYLXMUUHYLUULXM
        UUHYLUULXMWBUUHYLQUUKXMYHUUGYLUUKABGFECDWCWDWEVOWFWGYMYJYKQZYLQUUMYJYKY
        LWHUUNUULYLUUNUUIOZUUJOZQUULYJUUOYKUUPYIXPSYIXRSTUUIUUJRUAWIWJWKWLVQWMV
        OWNVDWOWPWQXEXGXCXEXDJKHJKBMXDHJWRBCWSWTXAXBWQ $.
        $( [8-Jul-2006] $) $( [15-Jun-2006] $)
    $}
  $}

  ${
    $d x y z A $.
    $( The Hilbert lattice is irreducible:  any element that commutes with all
       elements must be zero or one.  Theorem 14.8.4 of [BeltramettiCassinelli]
       p. 166. $)
    irredt $p |- ( ( A e. CH /\ A. x e. CH A C_H x ) ->
                 ( A = 0H \/ A = H~ ) ) $=
      ( vy vz cch wcel cv ccm wbr wral wa c0h wceq chil wo cif eqeq1 orbi12d
      eleq1 ax-17 hbra1 hban hbif hbeq breq1 ralbid anbi12d h0elch cm0t rgen
      pm3.2i elimhyp pm3.26i pm3.27i hbbr breq2 rcla4 mpi irred dedth ) BEFZBAG
      ZHIZAEJZKZBLMZBNMZOVEBLPZLMZVHNMZOBLBVHMZVFVIVGVJBVHLQBVHNQRCVHVHEFZVHVBH
      IZAEJZVEVLVNKLEFZLVBHIZAEJZKBLVKVAVLVDVNBVHESVKVCVMAEACCBVHCGZBFATZVEACBL
      VAVDAVAATVCAEUAUBZVSVRLFATZUCZUDBVHVBHUEUFUGLVHMZVOVLVQVNLVHESWCVPVMAEACC
      LVHWAWBUDLVHVBHUEUFUGVOVQUHVPAEVBUIUJUKULZUMVREFVNVHVRHIZVLVNWDUNVMWEAVRE
      ADVHVRHVEADBLVTDGZBFATWFLFATUCWFHFATWFVRFATUOVBVRVHHUPUQURUSUT $.
      $( [1-Oct-2006] $) $( [16-Jun-2006] $)
  $}

