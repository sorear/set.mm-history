$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Irreducibility
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d p q r x A $.
    irred.1 $e |- A e. CH $.
    $( Lemma for ~ irredi . $)
    irredlem1 $p |- ( ( ( p e. Atoms /\ ( q e. CH /\ q C_ ( _|_ ` A ) ) )
     /\ ( ( r e. Atoms /\ r C_ A ) /\ r C_ ( p vH q ) ) ) ->
       ( p i^i ( _|_ ` r ) ) = 0H ) $=
      ( cv cat wcel cch cort cfv wss wa chj co cin c0h wceq wi sstr2 wb
      chsscon3 mpan2 biimpa atelch sylan syl5 wn wne atn0 df-ne sylib adantr
      ad2antrr simplr chlej1 3exp1 choccl syl syl5com imp42 adantllr adantlr
      sstrd chlejb2 ancoms sylanl1 an1rs adantrl sseqtrd ex chssoc biimpd syld
      mtod sylanr1 atnssm0 incom eqeq1i syl6bb ad2ant2r sylibd exp43 sylcom
      com4t imp3a imp43 ) DFZGHZCFZIHZWJAJKZLZMBFZGHZWNALZMZWNWHWJNOZLZWHWNJKZP
      ZQRZWIWKWMWQWSXBSZSWMWQWIWKXCWMWQWJWTLZWIWKXCSSZWMWLWTLZXDWQWJWLWTTWNIHZW
      PXFWOXGWPXFXGAIHWPXFUAEWNAUBUCUDWNUEZUFUGWOXDXESWPWOXDWIWKXCWOXDMZWIWKMMW
      SWHWTLZUHZXBXIWHIHZWKWSXKSWIXIXLWKMZMZWSXKXNWSMZXJWNQRZXIXPUHZXMWSWOXQXDW
      OWNQUIXQWNUJWNQUKULUMUNXOXJWNWTLZXPXOXJXRXOXJMZWNWTWJNOZWTXSWNWRXTXNWSXJU
      OXNXJWRXTLZWSWOXMXJYAXDWOXLWKXJYAXLWTIHZWKXJYASSWOXLYBWKXJYAWHWTWJUPUQWOX
      GYBXHWNURUSZUTVAVBVCVDXNXTWTRZWSXJXIWKYDXLWOWKXDYDYBWKXDYDWOYBWKMXDYDWKYB
      XDYDUAWJWTVEVFUDYCVGVHVIUNVJVKXIXRXPSZXMWSWOYEXDWOXGYEXHXGXRXPWNVLVMUSUMU
      NVNVOVKWHUEVPWOWIXKXBUAZXDWKYBWIYFWOYBWIMXKWTWHPZQRXBWTWHVQYGXAQWTWHVRVSV
      TYCUFWAWBWCUMWDWEWFWG $.
      $( [8-Jul-2006] $) $( [14-Jun-2006] $)

    $( Lemma for ~ irredi . $)
    irredlem2 $p |- ( ( ( ( p e. Atoms /\ p C_ A ) /\ ( q e. CH /\
      q C_ ( _|_ ` A ) ) ) /\ ( ( r e. Atoms /\ r C_ A ) /\ r C_ ( p vH q ) ) )
                  -> ( ( _|_ ` r ) i^i ( p vH q ) ) = q ) $=
      ( cv cat wcel wss wa cch cort cfv chj co cin wceq chjcom atelch sylan
      ad2ant2r adantr ineq2d w3a ccm wbr fh2 choccl syl id 3anim123i 3com13
      3expa adantllr adantlrr adantrr lecm simpll ad2antrl sstr wb chsscon3
      mpan2 biimpa sylan2 adantll syl3anc ad2ant2lr an1s ancom2s wi syl3an2
      3expia cmcm2 sylibrd mpd sylanl2 ancom1s an4s jca sylanc c0h sseqin2
      sylib irredlem1 incom syl5eq opreq12d chj0 ad2antlr eqtrd 3eqtrd ) DFZGHZ
      WMAIZJZCFZKHZWQALMZIZJZJZBFZGHZXCAIZJZXCWMWQNOZIZJZJZXCLMZXGPXKWQWMNOZPZX
      KWQPZXKWMPZNOZWQXJXGXLXKXBXGXLQZXIWNWRXQWOWTWMKHZWRXQWNWMWQRWMSZTUAUBUCXK
      KHZWRXRUDZWQXKUEUFZWQWMUEUFZJXMXPQXJXKWQWMUGXBXFYAXHXBXDYAXEWPWRXDYAWTWNW
      RXDYAWOWNWRXDYAXDWRWNYAXDXTWRWRWNXRXDXCKHZXTXCSZXCUHUIZWRUJXSUKULUMUNUOUP
      UPXJYBYCXAXFYBWPXHWRXTWQXKIZYBXAXFJZWQXKUQWRWTXFURXDXTXAXEYFUSWTXFYGWRWTW
      SXKIZYGXFWQWSXKUTXDXEYIXDYDXEYIVAZYEYDAKHZYJEXCAVBVCUIVDVEVFZVGVHXBYCXIWN
      WRWOWTYCWRWNWOWTJZYCWRXRYMYCWNWRXRJZYMJWQWMLMZIZYCXRYMYPWRXRWTWOYPWTXRWOY
      PWTWSYOIZYPXRWOJWQWSYOUTXRWOYQXRYKWOYQVAEWMAVBVCVDVEVIVJVFYNYPYCVKYMYNYPW
      QYOUEUFZYCWRXRYPYRWRYOKHYPYRXRWQYOUQWMUHVLVMWQWMVNVOUBVPXSVQVRVSUBVTWAXJX
      PWQWBNOZWQXJXNWQXOWBNXAXFXNWQQZWPXHYHYGYTYLWQXKWCWDVHXJWMXKPZWBXOWNXAXIUU
      AWBQWOABCDEWEUNXKWMWFWGWHXAYSWQQZWPXIWRUUBWTWQWIUBWJWKWL $.
      $( [8-Jul-2006] $) $( [15-Jun-2006] $)

    ${
      irred.2 $e |- ( x e. CH -> A C_H x ) $.
      $( Lemma for ~ irredi . $)
      irredlem3 $p |- ( ( ( ( p e. Atoms /\ p C_ A ) /\ ( q e. Atoms /\
        q C_ ( _|_ ` A ) ) ) /\ ( r e. Atoms /\ r C_ ( p vH q ) ) )
                    -> ( r C_ A -> r = p ) ) $=
        ( cv cat wcel wss wa cort cfv chj co wceq wi cch c0h cin irredlem2
        opreq2d pjoml2 atelch adantr chjcl sylan ad2ant2r id syl3an 3com12
        3expb eqtr3d ineq2d ccm wbr breq2 vtoclga anim12i fh1 mp3anl1 mpdan
        ancoms adantrr adantll 3eqtr3d sseqin2 biimpi ad2antlr adantl csh chsh
        chshii orthin mpan2 syl imp incom syl5eq opreq12d ad2antrr chj0 exp44
        com34 sylanr1 imp32 ) EHZIJZWHBKZLZDHZIJZWLBMNKZLLCHZIJZWOWHWLOPZKZWOBK
        ZWOWHQZRZWKWLSJZWNWPWRXARRWMWKXBWNLZLZWPWSWRWTXDWPWSWRWTXDWPWSLZWRLZLZW
        OTOPZWHTOPZWOWHXGBWOUAZBWLUAZOPZBWHUAZXKOPZXHXIXGBWOWLOPZUAZBWQUAZXLXNX
        GXOWQBXGWOWOMNWQUAZOPZXOWQXGXRWLWOOBCDEFUBUCXDXEWRXSWQQZXEXDWRXTWOSJZWQ
        SJZWRXTXEXDWRWOWQUDWPYAWSWOUEZUFWIXBYBWJWNWHSJZXBYBWIWHWLUGWHUEZUHUIWRU
        JUKULUMUNUOXCXFXPXLQZWKXBXEYFWNWRXBWPYFWSWPXBYFYAXBYFWPYAXBLBWOUPUQZBWL
        UPUQZLZYFYAYGXBYHBAHZUPUQZYGAWOSYJWOBUPURGUSYKYHAWLSYJWLBUPURGUSZUTBSJZ
        YAXBYIYFFBWOWLVAVBVCYCUHVDVEUIVFXDXQXNQZXFWIXBYNWJWNYDXBYNWIYDXBLBWHUPU
        QZYHLZYNYDYOXBYHYKYOAWHSYJWHBUPURGUSYLUTYMYDXBYPYNFBWHWLVAVBVCYEUHUIUFV
        GXGXJWOXKTOXFXJWOQZXDWSYQWPWRWSYQWOBVHVIVJVKXCXKTQWKXFXCWLBUAZTXKXBWNYR
        TQZXBWLVLJZWNYSRZWLVMYTBVLJUUABFVNWLBVOVPVQVRBWLVSVTVJZWAXGXMWHXKTOWKXM
        WHQZXCXFWJUUCWIWJUUCWHBVHVIVKWBUUBWAVGXFXHWOQZXDWPUUDWSWRWPYAUUDYCWOWCV
        QWBVKWKXIWHQZXCXFWIUUEWJWIYDUUEYEWHWCVQUFWBVGWDWEWLUEWFWG $.
        $( [8-Jul-2006] $) $( [15-Jun-2006] $)

      $( Lemma for ~ irredi . $)
      irredlem4 $p |- ( ( ( ( p e. Atoms /\ p C_ A ) /\ ( q e. Atoms /\
        q C_ ( _|_ ` A ) ) ) /\ ( r e. Atoms /\
              r C_ ( p vH q ) ) ) -> ( r = p \/ r = q ) ) $=
        ( cv cat wcel wss wa cort cfv chj co wo wceq ccm wbr cch atelch breq2
        vtoclga syl atordi mpdan ad2antrl irredlem3 wi wb chjcom syl2an sseq2d
        anbi2d ad2ant2r choccli cmcm3 mpan mpbid ex sylbird ococi sseq2i
        biimpri sylanr2 imp ancom1s orim12d mpd ) EHZIJZVKBKZLZDHZIJZVOBMNZKZLZ
        LZCHZIJZWAVKVOOPZKZLZLZWABKZWAVQKZQZWAVKRZWAVORZQWBWIVTWDWBBWASTZWIWBWA
        UAJWLWAUBBAHZSTZWLAWAUAWMWABSUCGUDUEBWAFUFUGUHWFWGWJWHWKABCDEFGUIVSVNWE
        WHWKUJZVSVNLWEWOVSVLVKVQMNZKZWEWOUJVMVSVLWQLLZWEWBWAVOVKOPZKZLZWOVPVLXA
        WEUKVRWQVPVLLZWTWDWBXBWSWCWAVOUAJVKUAJWSWCRVPVLVOVKULVOUBVKUBUMUNUOUPWR
        XAWOAVQCEDBFUQWMUAJZWNVQWMSTZGBUAJXCWNXDUKFBWMURUSUTUIVAVBWQVMWPBVKBFVC
        VDVEVFVGVHVIVJ $.
        $( [8-Jul-2006] $) $( [15-Jun-2006] $)

      $( The Hilbert lattice is irreducible: any element that commutes with all
         elements must be zero or one.  Theorem 14.8.4 of
         [BeltramettiCassinelli] p. 166. $)
      irredi $p |- ( A = 0H \/ A = ~H ) $=
        ( vp vq vr c0h wceq cort cfv wo chil eqid wn wne wa ioran df-ne
        anbi12i bitr4i cv wss cat wrex hatomici choccli anim12i reeanv sylibr
        wcel chj co w3a superpos simpll simprl sstr cch wb atelch chsscon3
        mpan2 syl biimpa sylan2 ancoms atn0 adantr sseq1 bicomd chssoc
        sylan9bbr an1rs ex necon3d mpd adantlr syldan adantrl syl3anc wi
        irredlem4 anassrs pm2.24d com23 imp3a df-3an neanior anbi1i bitri
        syl5ib r19.23adva an4s r19.23aivv sylbi con4i ax-mp fveq2 ococi choc0
        3eqtr3g orim2i ) BHIZBJKZHIZLZXDBMIZLHHIZXGHNXGXIXGOZBHPZXEHPZQZXIOZXJX
        DOZXFOZQXMXDXFRXKXOXLXPBHSXEHSTUAXMEUBZBUCZFUBZXEUCZQZFUDUEEUDUEZXNXMXR
        EUDUEZXTFUDUEZQYBXKYCXLYDEBCUFFXEBCUGUFUHXRXTEFUDUDUIUJYAXNEFUDUDXQUDUK
        ZXSUDUKZQYAXNYEXRYFXTXNYEXRQZYFXTQZQZGUBZXQPZYJXSPZYJXQXSULUMUCZUNZGUDU
        EZXNYEYFXQXSPZYOYIGXQXSUOYEXRYHUPYGYFXTUQYGXTYPYFYGXTXSXQJKZUCZYPXTYGYR
        XTXEYQUCZYRYGXSXEYQURYEXRYSYEXQUSUKZXRYSUTZXQVAZYTBUSUKUUACXQBVBVCVDVEV
        FVGYEYRYPXRYEYRQZXQHPZYPYEUUDYRXQVHVIUUCXQXSXQHUUCXQXSIZXQHIZYEUUEYRUUF
        YEUUEQYRUUFUUEYRXQYQUCZYEUUFUUEUUGYRXQXSYQVJVKYEYTUUGUUFUTUUBXQVLVDVMVE
        VNVOVPVQVRVSVTWAYIYNXNGUDYIYJUDUKZQZYJXQIYJXSILZOZYMQZXNYNUUIUUKYMXNUUI
        YMUUKXNUUIYMUUKXNWBUUIYMQUUJXNYIUUHYMUUJABGFECDWCWDWEVOWFWGYNYKYLQZYMQU
        ULYKYLYMWHUUMUUKYMYJXQYJXSWIWJWKWLWMVQWNVOWOVDWPWQWRXFXHXDXFXEJKHJKBMXE
        HJWSBCWTXAXBXCWR $.
        $( [8-Jul-2006] $) $( [15-Jun-2006] $)
    $}
  $}

  ${
    $d x y z A $.
    $( The Hilbert lattice is irreducible: any element that commutes with all
       elements must be zero or one.  Theorem 14.8.4 of [BeltramettiCassinelli]
       p. 166. $)
    irred $p |- ( ( A e. CH /\ A. x e. CH A C_H x ) ->
                 ( A = 0H \/ A = ~H ) ) $=
      ( vy vz cch wcel cv ccm wbr wral wa c0h wceq chil wo cif eqeq1 orbi12d
      eleq1 ax-17 hbra1 hban hbif hbeq breq1 ralbid anbi12d h0elch cm0 rgen
      pm3.2i elimhyp pm3.26i pm3.27i hbbr breq2 rcla4 mpi irredi dedth ) BEFZBA
      GZHIZAEJZKZBLMZBNMZOVEBLPZLMZVHNMZOBLBVHMZVFVIVGVJBVHLQBVHNQRCVHVHEFZVHVB
      HIZAEJZVEVLVNKLEFZLVBHIZAEJZKBLVKVAVLVDVNBVHESVKVCVMAEACCBVHCGZBFATZVEACB
      LVAVDAVAATVCAEUAUBZVSVRLFATZUCZUDBVHVBHUEUFUGLVHMZVOVLVQVNLVHESWCVPVMAEAC
      CLVHWAWBUDLVHVBHUEUFUGVOVQUHVPAEVBUIUJUKULZUMVREFVNVHVRHIZVLVNWDUNVMWEAVR
      EADVHVRHVEADBLVTDGZBFATWFLFATUCWFHFATWFVRFATUOVBVRVHHUPUQURUSUT $.
      $( [1-Oct-2006] $) $( [16-Jun-2006] $)
  $}

