$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Irreducibility
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d p q r x A $.
    irred.1 $e |- A e. CH $.
    $( Lemma for ~ irredi . $)
    irredlem1 $p |- ( ( ( p e. HAtoms /\ ( q e. CH /\ q C_ ( _|_ ` A ) ) )
     /\ ( ( r e. HAtoms /\ r C_ A ) /\ r C_ ( p vH q ) ) ) ->
       ( p i^i ( _|_ ` r ) ) = 0H ) $=
      ( cv cat wcel cch cort cfv wss wa chj co cin c0h wceq wi atelch wb
      chsscon3 mpan2 biimpa sylan sstr2 syl5 wn wne atne0 df-ne sylib
      ad3antrrr simplr choccl syl chlej1 3exp1 syl5com imp42 adantllr adantlr
      sstrd chlejb2 ancoms sylanl1 an32s adantrl ad2antrr sseqtrd ex chssoc
      biimpd syld mtod sylanr1 atnssm0 incom eqeq1i syl6bb ad2ant2r sylibd
      exp43 adantr sylcom com4t imp3a imp43 ) DFZGHZCFZIHZWKAJKZLZMBFZGHZWOALZM
      ZWOWIWKNOZLZWIWOJKZPZQRZWJWLWNWRWTXCSZSWNWRWJWLXDWNWRWKXALZWJWLXDSSZWRWMX
      ALZWNXEWPWOIHZWQXGWOTZXHWQXGXHAIHWQXGUAEWOAUBUCUDUEWKWMXAUFUGWPXEXFSWQWPX
      EWJWLXDWPXEMZWJWLMMWTWIXALZUHZXCWJXJWIIHZWLWTXLSWITXJXMWLMZMZWTXLXOWTMZXK
      WOQRZWPXQUHZXEXNWTWPWOQUIXRWOUJWOQUKULUMXPXKWOXALZXQXPXKXSXPXKMZWOXAWKNOZ
      XAXTWOWSYAXOWTXKUNXOXKWSYALZWTWPXNXKYBXEWPXMWLXKYBWPXAIHZXMWLXKYBSSWPXHYC
      XIWOUOUPZXMYCWLXKYBWIXAWKUQURUSUTVAVBVCXOYAXARZWTXKXJWLYEXMWPWLXEYEWPYCWL
      XEYEYDYCWLMXEYEWLYCXEYEUAWKXAVDVEUDVFVGVHVIVJVKWPXSXQSZXEXNWTWPXHYFXIXHXS
      XQWOVLVMUPUMVNVOVKVPWPWJXLXCUAZXEWLWPYCWJYGYDYCWJMXLXAWIPZQRXCXAWIVQYHXBQ
      XAWIVRVSVTUEWAWBWCWDWEWFWGWH $.
      $( [14-Jun-2006] $)

    $( Lemma for ~ irredi . $)
    irredlem2 $p |- ( ( ( ( p e. HAtoms /\ p C_ A ) /\ ( q e. CH /\
      q C_ ( _|_ ` A ) ) ) /\ ( ( r e. HAtoms /\ r C_ A ) /\ r C_ ( p vH q ) )
        )
                  -> ( ( _|_ ` r ) i^i ( p vH q ) ) = q ) $=
      ( cv cat wcel wss wa cch cort cfv chj co cin wceq atelch chjcom sylan
      ad2ant2r adantr ineq2d w3a ccm wbr choccl syl id 3anim123i 3com13 3expa
      adantllr adantlrr adantrr simpll ad2antrl wb chsscon3 sylancl biimpa
      sstr sylan2 adantll lecm syl111anc ad2ant2lr mpan2 an12s ancom2s wi
      syl3an2 3expia cmcm2 sylibrd mpd sylanl2 ancom1s an4s fh2 syl12anc c0h
      sseqin2 sylib incom irredlem1 syl5eq opreq12d chj0 ad2antlr eqtrd 3eqtrd
      ) DFZGHZWMAIZJZCFZKHZWQALMZIZJZJZBFZGHZXCAIZJZXCWMWQNOZIZJZJZXCLMZXGPXKWQ
      WMNOZPZXKWQPZXKWMPZNOZWQXJXGXLXKXBXGXLQZXIWNWRXQWOWTWNWMKHZWRXQWMRZWMWQST
      UAUBUCXJXKKHZWRXRUDZWQXKUEUFZWQWMUEUFZXMXPQXBXFYAXHXBXDYAXEWPWRXDYAWTWNWR
      XDYAWOWNWRXDYAXDWRWNYAXDXTWRWRWNXRXDXCKHZXTXCRZXCUGUHZWRUIXSUJUKULUMUNUOU
      OXAXFYBWPXHXAXFJZWRXTWQXKIZYBWRWTXFUPXDXTXAXEYFUQWTXFYHWRXFWTWSXKIZYHXDXE
      YIXDYDAKHZXEYIURYEEXCAUSUTVAWQWSXKVBVCVDZWQXKVEVFVGXBYCXIWNWRWOWTYCWRWNWO
      WTJZYCWNWRXRYLYCXSWRXRJZYLJWQWMLMZIZYCXRYLYOWRXRWTWOYOWTXRWOYOXRWOJWTWSYN
      IZYOXRWOYPXRYJWOYPUREWMAUSVHVAWQWSYNVBVCVIVJVDYMYOYCVKYLYMYOWQYNUEUFZYCWR
      XRYOYQXRWRYNKHYOYQWMUGWQYNVEVLVMWQWMVNVOUBVPVQVRVSUBXKWQWMVTWAXJXPWQWBNOZ
      WQXJXNWQXOWBNXAXFXNWQQZWPXHYGYHYSYKWQXKWCWDVGXJXOWMXKPZWBXKWMWEWNXAXIYTWB
      QWOABCDEWFUMWGWHXAYRWQQZWPXIWRUUAWTWQWIUBWJWKWL $.
      $( [15-Jun-2006] $)

    ${
      irred.2 $e |- ( x e. CH -> A C_H x ) $.
      $( Lemma for ~ irredi . $)
      irredlem3 $p |- ( ( ( ( p e. HAtoms /\ p C_ A ) /\ ( q e. HAtoms /\
        q C_ ( _|_ ` A ) ) ) /\ ( r e. HAtoms /\ r C_ ( p vH q ) ) )
                    -> ( r C_ A -> r = p ) ) $=
        ( cv cat wcel wss wa cort cfv chj co wceq wi cch atelch c0h cin
        irredlem2 opreq2d adantr chjcl sylan ad2ant2r id pjoml2 syl3an 3com12
        3expb eqtr3d ineq2d ccm wbr breq2 vtoclga anim12i fh1 mp3anl1 mpdan
        ancoms adantrr adantll 3eqtr3d sseqin2 biimpi ad2antlr adantl incom
        csh chsh chshii orthin sylancl imp syl5eq opreq12d ad2antrr chj0 syl
        ad3antrrr exp44 com34 sylanr1 imp32 ) EHZIJZWIBKZLZDHZIJZWMBMNKZLLCHZIJ
        ZWPWIWMOPZKZWPBKZWPWIQZRZWNWLWMSJZWOWQWSXBRRWMTWLXCWOLZLZWQWTWSXAXEWQWT
        WSXAXEWQWTLZWSLZLZWPUAOPZWIUAOPZWPWIXHBWPUBZBWMUBZOPZBWIUBZXLOPZXIXJXHB
        WPWMOPZUBZBWRUBZXMXOXHXPWRBXHWPWPMNWRUBZOPZXPWRXHXSWMWPOBCDEFUCUDXEXFWS
        XTWRQZXFXEWSYAXFWPSJZXEWRSJZWSWSYAWQYBWTWPTZUEWJXCYCWKWOWJWISJZXCYCWITZ
        WIWMUFUGUHWSUIWPWRUJUKULUMUNUOXDXGXQXMQZWLXCXFYGWOWSXCWQYGWTWQXCYGWQYBX
        CYGYDYBXCLBWPUPUQZBWMUPUQZLZYGYBYHXCYIBAHZUPUQZYHAWPSYKWPBUPURGUSYLYIAW
        MSYKWMBUPURGUSZUTBSJZYBXCYJYGFBWPWMVAVBVCUGVDVEUHVFXEXRXOQZXGWJXCYOWKWO
        WJYEXCYOYFYEXCLBWIUPUQZYILZYOYEYPXCYIYLYPAWISYKWIBUPURGUSYMUTYNYEXCYQYO
        FBWIWMVAVBVCUGUHUEVGXHXKWPXLUAOXGXKWPQZXEWTYRWQWSWTYRWPBVHVIVJVKXDXLUAQ
        WLXGXDXLWMBUBZUABWMVLXCWOYSUAQZXCWMVMJBVMJWOYTRWMVNBFVOWMBVPVQVRVSVJZVT
        XHXNWIXLUAOWLXNWIQZXDXGWKUUBWJWKUUBWIBVHVIVKWAUUAVTVGXGXIWPQZXEWQUUCWTW
        SWQYBUUCYDWPWBWCWAVKWJXJWIQZWKXDXGWJYEUUDYFWIWBWCWDVGWEWFWGWH $.
        $( [15-Jun-2006] $)

      $( Lemma for ~ irredi . $)
      irredlem4 $p |- ( ( ( ( p e. HAtoms /\ p C_ A ) /\ ( q e. HAtoms /\
        q C_ ( _|_ ` A ) ) ) /\ ( r e. HAtoms /\
              r C_ ( p vH q ) ) ) -> ( r = p \/ r = q ) ) $=
        ( cv cat wcel wss wa cort cfv chj co wo wceq ccm wbr cch atelch breq2
        vtoclga syl atordi mpdan ad2antrl irredlem3 wi ococi sseq2i biimpri wb
        chjcom syl2an sseq2d anbi2d ad2ant2r choccli cmcm3 mpan mpbid ex
        sylbird sylanr2 imp ancom1s orim12d mpd ) EHZIJZVKBKZLZDHZIJZVOBMNZKZLZ
        LZCHZIJZWAVKVOOPZKZLZLZWABKZWAVQKZQZWAVKRZWAVORZQWBWIVTWDWBBWASTZWIWBWA
        UAJWLWAUBBAHZSTZWLAWAUAWMWABSUCGUDUEBWAFUFUGUHWFWGWJWHWKABCDEFGUIVSVNWE
        WHWKUJZVSVNLWEWOVMVSVLVKVQMNZKZWEWOUJWQVMWPBVKBFUKULUMVSVLWQLLZWEWBWAVO
        VKOPZKZLZWOVPVLXAWEUNVRWQVPVLLZWTWDWBXBWSWCWAVPVOUAJVKUAJWSWCRVLVOUBVKU
        BVOVKUOUPUQURUSWRXAWOAVQCEDBFUTWMUAJZWNVQWMSTZGBUAJXCWNXDUNFBWMVAVBVCUI
        VDVEVFVGVHVIVJ $.
        $( [15-Jun-2006] $)

      $( The Hilbert lattice is irreducible: any element that commutes with all
         elements must be zero or one.  Theorem 14.8.4 of
         [BeltramettiCassinelli] p. 166. $)
      irredi $p |- ( A = 0H \/ A = ~H ) $=
        ( vp vq vr c0h wceq cort cfv wo chil eqid wn wne wa ioran df-ne
        anbi12i bitr4i cv wss cat wrex hatomici choccli anim12i reeanv sylibr
        wcel chj co w3a simpll simprl cch wb atelch chsscon3 sylancl biimpa
        sstr sylan2 ancoms atne0 adantr sseq1 bicomd chssoc syl sylan9bbr
        an32s ex necon3d mpd adantlr syldan adantrl superpos syl111anc df-3an
        neanior anbi1i bitri wi irredlem4 anassrs pm2.24d com23 imp3a syl5bi
        r19.23adva an4s r19.23aivv sylbi mt4 fveq2 ococi choc0 3eqtr3g orim2i
        ax-mp ) BHIZBJKZHIZLZXDBMIZLHHIZXGHNXGOZBHPZXEHPZQZXIOZXJXDOZXFOZQXMXDX
        FRXKXOXLXPBHSXEHSTUAXMEUBZBUCZFUBZXEUCZQZFUDUEEUDUEZXNXMXREUDUEZXTFUDUE
        ZQYBXKYCXLYDEBCUFFXEBCUGUFUHXRXTEFUDUDUIUJYAXNEFUDUDXQUDUKZXSUDUKZQYAXN
        YEXRYFXTXNYEXRQZYFXTQZQZGUBZXQPZYJXSPZYJXQXSULUMUCZUNZGUDUEZXNYIYEYFXQX
        SPZYOYEXRYHUOYGYFXTUPYGXTYPYFYGXTXSXQJKZUCZYPXTYGYRYGXTXEYQUCZYRYEXRYSY
        EXQUQUKZBUQUKXRYSURXQUSZCXQBUTVAVBXSXEYQVCVDVEYEYRYPXRYEYRQZXQHPZYPYEUU
        CYRXQVFVGUUBXQXSXQHUUBXQXSIZXQHIZYEUUDYRUUEYEUUDQYRUUEUUDYRXQYQUCZYEUUE
        UUDUUFYRXQXSYQVHVIYEYTUUFUUEURUUAXQVJVKVLVBVMVNVOVPVQVRVSGXQXSVTWAYIYNX
        NGUDYNYJXQIYJXSILZOZYMQZYIYJUDUKZQZXNYNYKYLQZYMQUUIYKYLYMWBUULUUHYMYJXQ
        YJXSWCWDWEUUKUUHYMXNUUKYMUUHXNUUKYMUUHXNWFUUKYMQUUGXNYIUUJYMUUGABGFECDW
        GWHWIVNWJWKWLWMVPWNVNWOVKWPWQXFXHXDXFXEJKHJKBMXEHJWRBCWSWTXAXBXC $.
        $( [15-Jun-2006] $)
    $}
  $}

  ${
    $d x y z A $.
    $( The Hilbert lattice is irreducible: any element that commutes with all
       elements must be zero or one.  Theorem 14.8.4 of [BeltramettiCassinelli]
       p. 166. $)
    irred $p |- ( ( A e. CH /\ A. x e. CH A C_H x ) ->
                 ( A = 0H \/ A = ~H ) ) $=
      ( vy vz cch wcel cv ccm wbr wral wa c0h wceq chil wo cif eqeq1 orbi12d
      eleq1 ax-17 hbra1 hban hbif hbeq breq1 ralbid anbi12d h0elch cm0 rgen
      pm3.2i elimhyp simpli simpri hbbr breq2 rcla4 mpi irredi dedth ) BEFZBAGZ
      HIZAEJZKZBLMZBNMZOVEBLPZLMZVHNMZOBLBVHMZVFVIVGVJBVHLQBVHNQRCVHVHEFZVHVBHI
      ZAEJZVEVLVNKLEFZLVBHIZAEJZKBLVKVAVLVDVNBVHESVKVCVMAEACCBVHCGZBFATZVEACBLV
      AVDAVAATVCAEUAUBZVSVRLFATZUCZUDBVHVBHUEUFUGLVHMZVOVLVQVNLVHESWCVPVMAEACCL
      VHWAWBUDLVHVBHUEUFUGVOVQUHVPAEVBUIUJUKULZUMVREFVNVHVRHIZVLVNWDUNVMWEAVREA
      DVHVRHVEADBLVTDGZBFATWFLFATUCWFHFATWFVRFATUOVBVRVHHUPUQURUSUT $.
      $( [16-Jun-2006] $)
  $}

