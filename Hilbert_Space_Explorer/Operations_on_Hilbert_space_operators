$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Operations on Hilbert space operators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A $.  $d x y S $.  $d x y T $.
    $( The sum of Hilbert space operators is an operator. $)
    hoaddcl $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( S +op T ) : ~H --> ~H ) $=
      ( vx vy chil wf wa chos co cv wcel cfv cva wceq copab wral ffvelrn
      hvaddcl syl2an anandirs r19.21aiva eqid fopab2 sylib hosmval feq1d
      mpbird ) EEAFZEEBFZGZEEABHIZFEECJZEKZDJULALZULBLZMIZNGCDOZFZUJUPEKZCEPURU
      JUSCEUHUIUMUSUHUMGUNEKUOEKUSUIUMGEEULAQEEULBQUNUORSTUACDEEUPUQUQUBUCUDUJE
      EUKUQCDABUEUFUG $.
      $( [21-Feb-2006] $)

    $( The scalar product of a Hilbert space operator is an operator. $)
    homulcl $p |- ( ( A e. CC /\ T : ~H --> ~H ) ->
                   ( A .op T ) : ~H --> ~H ) $=
      ( vx vy cc wcel chil wf wa chot co cv cfv csm wceq copab wral ffvelrn
      hvmulcl sylan2 anassrs r19.21aiva eqid fopab2 sylib hommval feq1d mpbird
      ) AEFZGGBHZIZGGABJKZHGGCLZGFZDLAUMBMZNKZOICDPZHZUKUPGFZCGQURUKUSCGUIUJUNU
      SUJUNIUIUOGFUSGGUMBRAUOSTUAUBCDGGUPUQUQUCUDUEUKGGULUQCDABUFUGUH $.
      $( [21-Feb-2006] $)
  $}

  ${
    $d x T $.  $d x U $.
    $( Equality of Hilbert space operators. $)
    hoeq $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                ( A. x e. ~H ( T ` x ) = ( U ` x ) <-> T = U ) ) $=
      ( chil wf wfn cv cfv wceq wral wb ffn wa eqfnfv2 bicomd syl2an ) DDBEBDFZ
      CDFZAGZBHSCHIADJZBCIZKDDCEDDBLDDCLQRMUATADBCNOP $.
      $( [12-Aug-2006] $)
  $}

  ${
    $d x y S $.  $d x y T $.
    hoeq.1 $e |- S : ~H --> ~H $.
    hoeq.2 $e |- T : ~H --> ~H $.
    $( Equality of Hilbert space operators. $)
    hoeqi $p |- ( A. x e. ~H ( S ` x ) = ( T ` x ) <-> S = T ) $=
      ( chil wf cv cfv wceq wral wb hoeq mp2an ) FFBGFFCGAHZBIOCIJAFKBCJLDEABCM
      N $.
      $( [14-Nov-2000] $)

    $( Closure of Hilbert space operator sum. $)
    hoscli $p |- ( A e. ~H -> ( ( S +op T ) ` A ) e. ~H ) $=
      ( chil wf wcel chos co cfv hoscl mpanl12 ) FFBGFFCGAFHABCIJKFHDEABCLM $.
      $( [12-Nov-2000] $)

    $( Closure of Hilbert space operator difference. $)
    hodcli $p |- ( A e. ~H -> ( ( S -op T ) ` A ) e. ~H ) $=
      ( chil wf wcel chod co cfv hodcl mpanl12 ) FFBGFFCGAFHABCIJKFHDEABCLM $.
      $( [12-Nov-2000] $)

    $( Composition of Hilbert space operators. $)
    hocoi $p |- ( A e. ~H -> ( ( S o. T ) ` A ) = ( S ` ( T ` A ) ) ) $=
      ( chil wcel cdm ccom cfv wceq fdmi eleq2i wfun wf ffun ax-mp fvco
      mp3an12 sylbir ) AFGACHZGZABCIJACJBJKZUAFAFFCELMBNZCNZUBUCFFBOUDDFFBPQFFC
      OUEEFFCPQABCRST $.
      $( [12-Nov-2000] $)

    $( Closure of composition of Hilbert space operators. $)
    hococli $p |- ( A e. ~H -> ( ( S o. T ) ` A ) e. ~H ) $=
      ( chil wcel ccom cfv hocoi ffvelrni syl eqeltrd ) AFGZABCHIACIZBIZFABCDEJ
      NOFGPFGFFACEKFFOBDKLM $.
      $( [12-Nov-2000] $)

    $( Mapping of composition of Hilbert space operators. $)
    hocofi $p |- ( S o. T ) : ~H --> ~H $=
      ( chil wf ccom fco mp2an ) EEAFEEBFEEABGFCDEEEABHI $.
      $( [14-Nov-2000] $)

    $( Functionality of composition of Hilbert space operators. $)
    hocofni $p |- ( S o. T ) Fn ~H $=
      ( chil ccom wf wfn hocofi ffn ax-mp ) EEABFZGLEHABCDIEELJK $.
      $( [12-Nov-2000] $)

    $( Mapping of sum of Hilbert space operators. $)
    hoaddcli $p |- ( S +op T ) : ~H --> ~H $=
      ( chil wf chos co hoaddcl mp2an ) EEAFEEBFEEABGHFCDABIJ $.
      $( [14-Nov-2000] $)

    $( Mapping of difference of Hilbert space operators. $)
    hosubcli $p |- ( S -op T ) : ~H --> ~H $=
      ( vx vy chil cv cfv cmv co chod wf wcel wceq wa copab hodmval mp2an
      ffvelrni hvsubcl syl11anc fopab ) EFGGEHZAIZUDBIZJKZABLKZGGAMGGBMUHUDGNZF
      HUGOPEFQOCDEFABRSUIUEGNUFGNUGGNGGUDACTGGUDBDTUEUFUAUBUC $.
      $( [14-Nov-2000] $)

    $( Functionality of sum of Hilbert space operators. $)
    hoaddfni $p |- ( S +op T ) Fn ~H $=
      ( chil chos co wf wfn hoaddcli ffn ax-mp ) EEABFGZHMEIABCDJEEMKL $.
      $( [14-Nov-2000] $)

    $( Functionality of difference of Hilbert space operators. $)
    hosubfni $p |- ( S -op T ) Fn ~H $=
      ( chil chod co wf wfn hosubcli ffn ax-mp ) EEABFGZHMEIABCDJEEMKL $.
      $( [2-Jun-2006] $)

    $( Commutativity of sum of Hilbert space operators. $)
    hoaddcomi $p |- ( S +op T ) = ( T +op S ) $=
      ( vx cv chos co cfv wceq chil wral wcel cva ffvelrni ax-hvcom syl11anc
      wf hosval mp3an12 3eqtr4d rgen hoaddcli hoeqi mpbi ) EFZABGHZIZUFBAGHZIZJ
      ZEKLUGUIJUKEKUFKMZUFAIZUFBIZNHZUNUMNHZUHUJULUMKMUNKMUOUPJKKUFACOKKUFBDOUM
      UNPQKKARZKKBRZULUHUOJCDUFABSTURUQULUJUPJDCUFBASTUAUBEUGUIABCDUCBADCUCUDUE
      $.
      $( [15-Nov-2000] $)
  $}

  $( Mapping of difference of Hilbert space operators. $)
  hosubcl $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
              ( S -op T ) : ~H --> ~H ) $=
    ( chil wf chod co ch0o cif wceq opreq1 feq1d opreq2 ho0f elimf hosubcli
    dedth2h ) CCADZCCBDZCCABEFZDCCQAGHZBEFZDCCTRBGHZEFZDABGGATICCSUAATBEJKBUBIC
    CUAUCBUBTELKTUBCCAGMNCCBGMNOP $.
    $( [23-Aug-2006] $)

  $( Commutativity of sum of Hilbert space operators. $)
  hoaddcom $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( S +op T ) = ( T +op S ) ) $=
    ( chil wf chos co wceq ch0o cif opreq1 opreq2 eqeq12d ho0f elimf hoaddcomi
    dedth2h ) CCADZCCBDZABEFZBAEFZGQAHIZBEFZBUAEFZGUARBHIZEFZUDUAEFZGABHHAUAGSU
    BTUCAUABEJAUABEKLBUDGUBUEUCUFBUDUAEKBUDUAEJLUAUDCCAHMNCCBHMNOP $.
    $( [24-Aug-2006] $)

  ${
    $d x R $.  $d x S $.  $d x T $.
    hods.1 $e |- R : ~H --> ~H $.
    hods.2 $e |- S : ~H --> ~H $.
    hods.3 $e |- T : ~H --> ~H $.
    $( Relationship between Hilbert space operator difference and sum. $)
    hodsi $p |- ( ( R -op S ) = T <-> ( S +op T ) = R ) $=
      ( vx cv chod co cfv wceq chil wral chos wcel cmv cva wb ffvelrni
      hvsubadd syl111anc wf hodval mp3an12 eqeq1d hosval 3bitr4d ralbiia
      hosubcli hoeqi hoaddcli 3bitr3i ) GHZABIJZKZUNCKZLZGMNUNBCOJZKZUNAKZLZGMN
      UOCLUSALURVBGMUNMPZVAUNBKZQJZUQLZVDUQRJZVALZURVBVCVAMPVDMPUQMPVFVHSMMUNAD
      TMMUNBETMMUNCFTVAVDUQUAUBVCUPVEUQMMAUCMMBUCZVCUPVELDEUNABUDUEUFVCUTVGVAVI
      MMCUCVCUTVGLEFUNBCUGUEUFUHUIGUOCABDEUJFUKGUSABCEFULDUKUM $.
      $( [17-Nov-2000] $)

    $( Associativity of sum of Hilbert space operators. $)
    hoaddassi $p |- ( ( R +op S ) +op T ) = ( R +op ( S +op T ) ) $=
      ( vx cv chos co cfv wceq chil wral wcel cva ffvelrni ax-hvass syl111anc
      wf hoaddcli hosval mp3an12 opreq1d eqtrd opreq2d 3eqtr4d rgen hoeqi mpbi
      ) GHZABIJZCIJZKZUKABCIJZIJZKZLZGMNUMUPLURGMUKMOZUKAKZUKBKZPJZUKCKZPJZUTVA
      VCPJZPJZUNUQUSUTMOVAMOVCMOVDVFLMMUKADQMMUKBEQMMUKCFQUTVAVCRSUSUNUKULKZVCP
      JZVDMMULTMMCTZUSUNVHLABDEUAZFUKULCUBUCUSVGVBVCPMMATZMMBTZUSVGVBLDEUKABUBU
      CUDUEUSUQUTUKUOKZPJZVFVKMMUOTUSUQVNLDBCEFUAZUKAUOUBUCUSVMVEUTPVLVIUSVMVEL
      EFUKBCUBUCUFUEUGUHGUMUPULCVJFUAAUODVOUAUIUJ $.
      $( [26-Nov-2000] $)

    $( Commutative/associative law for Hilbert space operator sum that swaps
       the first two terms. $)
    hoadd12i $p |- ( R +op ( S +op T ) ) = ( S +op ( R +op T ) ) $=
      ( chos co hoaddcomi opreq1i hoaddassi 3eqtr3i ) ABGHZCGHBAGHZCGHABCGHGHBA
      CGHGHMNCGABDEIJABCDEFKBACEDFKL $.
      $( [27-Aug-2004] $)

    $( Commutative/associative law for Hilbert space operator sum that swaps
       the second and third terms. $)
    hoadd23i $p |- ( ( R +op S ) +op T ) = ( ( R +op T ) +op S ) $=
      ( chos co hoaddcomi opreq2i hoaddassi 3eqtr4i ) ABCGHZGHACBGHZGHABGHCGHAC
      GHBGHMNAGBCEFIJABCDEFKACBDFEKL $.
      $( [27-Jul-2006] $)

    $( Distributive law for Hilbert space operator sum. $)
    hocadddiri $p |- ( ( R +op S ) o. T ) = ( ( R o. T ) +op ( S o. T ) ) $=
      ( vx cv chos co ccom cfv wceq chil wral wcel cva ffvelrni wf hosval
      mp3an12 syl hocoi opreq12d eqtr4d hoaddcli hocofi 3eqtr4d rgen hoeqi
      mpbi ) GHZABIJZCKZLZULACKZBCKZIJZLZMZGNOUNURMUTGNULNPZULCLZUMLZULUPLZULUQ
      LZQJZUOUSVAVCVBALZVBBLZQJZVFVAVBNPZVCVIMZNNULCFRNNASNNBSVJVKDEVBABTUAUBVA
      VDVGVEVHQULACDFUCULBCEFUCUDUEULUMCABDEUFZFUCNNUPSNNUQSVAUSVFMACDFUGZBCEFU
      GZULUPUQTUAUHUIGUNURUMCVLFUGUPUQVMVNUFUJUK $.
      $( [26-Nov-2000] $)

    $( Distributive law for Hilbert space operator difference. $)
    hocsubdiri $p |- ( ( R -op S ) o. T ) = ( ( R o. T ) -op ( S o. T ) ) $=
      ( vx cv chod co ccom cfv wceq chil wral wcel cmv ffvelrni wf hodval
      mp3an12 syl hocoi opreq12d eqtr4d hosubcli hocofi 3eqtr4d rgen hoeqi
      mpbi ) GHZABIJZCKZLZULACKZBCKZIJZLZMZGNOUNURMUTGNULNPZULCLZUMLZULUPLZULUQ
      LZQJZUOUSVAVCVBALZVBBLZQJZVFVAVBNPZVCVIMZNNULCFRNNASNNBSVJVKDEVBABTUAUBVA
      VDVGVEVHQULACDFUCULBCEFUCUDUEULUMCABDEUFZFUCNNUPSNNUQSVAUSVFMACDFUGZBCEFU
      GZULUPUQTUAUHUIGUNURUMCVLFUGUPUQVMVNUFUJUK $.
      $( [26-Nov-2000] $)

    $( Double composition of Hilbert space operators. $)
    ho2coi $p |- ( A e. ~H -> ( ( ( R o. S ) o. T ) ` A ) =
                 ( R ` ( S ` ( T ` A ) ) ) ) $=
      ( chil wcel ccom cfv hocofi hocoi wceq ffvelrni syl eqtrd ) AHIZABCJZDJKA
      DKZSKZTCKBKZASDBCEFLGMRTHIUAUBNHHADGOTBCEFMPQ $.
      $( [1-Dec-2000] $)
  $}

  $( Associativity of sum of Hilbert space operators. $)
  hoaddass $p |- ( ( R : ~H --> ~H /\ S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( ( R +op S ) +op T ) = ( R +op ( S +op T ) ) ) $=
    ( chil wf chos co wceq ch0o cif opreq1 opreq1d eqeq12d opreq2 opreq2d ho0f
    elimf hoaddassi dedth3h ) DDAEZDDBEZDDCEZABFGZCFGZABCFGZFGZHTAIJZBFGZCFGZUG
    UEFGZHUGUABIJZFGZCFGZUGUKCFGZFGZHULUBCIJZFGZUGUKUPFGZFGZHABCIIIAUGHZUDUIUFU
    JUTUCUHCFAUGBFKLAUGUEFKMBUKHZUIUMUJUOVAUHULCFBUKUGFNLVAUEUNUGFBUKCFKOMCUPHZ
    UMUQUOUSCUPULFNVBUNURUGFCUPUKFNOMUGUKUPDDAIPQDDBIPQDDCIPQRS $.
    $( [24-Aug-2006] $)

  $( Commutative/associative law for Hilbert space operator sum that swaps the
     second and third terms. $)
  hoadd23 $p |- ( ( R : ~H --> ~H /\ S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( ( R +op S ) +op T ) = ( ( R +op T ) +op S ) ) $=
    ( chil wf w3a chos co wceq hoaddcom 3adant1 opreq2d hoaddass 3com23
    3eqtr4d ) DDAEZDDBEZDDCEZFZABCGHZGHACBGHZGHZABGHCGHACGHBGHZSTUAAGQRTUAIPBCJ
    KLABCMPRQUCUBIACBMNO $.
    $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a sum of Hilbert space operators. $)
  hoadd4 $p |- ( ( ( R : ~H --> ~H /\ S : ~H --> ~H ) /\
      ( T : ~H --> ~H /\ U : ~H --> ~H ) ) -> ( ( R +op S ) +op ( T +op U ) ) =
                ( ( R +op T ) +op ( S +op U ) ) ) $=
    ( chil wf wa chos co wceq w3a hoadd23 opreq1d 3expa adantrr hoaddcl
    hoaddass 3expb sylan an4s 3eqtr3d ) EEAFZEEBFZGZEECFZEEDFZGZGABHIZCHIZDHIZA
    CHIZBHIZDHIZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUDEEU
    HFZUGUJUNJZABPUQUEUFURUHCDQRSUBUEUCUFUMUOJZUBUEGEEUKFZUCUFGUSACPUTUCUFUSUKB
    DQRSTUA $.
    $( [24-Aug-2006] $)

  $( Distributive law for Hilbert space operator difference. $)
  hocsubdir $p |- ( ( R : ~H --> ~H /\ S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( ( R -op S ) o. T ) = ( ( R o. T ) -op ( S o. T ) ) ) $=
    ( chil wf chod co ccom wceq ch0o cif opreq1 coeq1d coeq1 opreq1d eqeq12d
    opreq2 opreq2d coeq2 opreq12d ho0f elimf hocsubdiri dedth3h ) DDAEZDDBEZDDC
    EZABFGZCHZACHZBCHZFGZIUEAJKZBFGZCHZUMCHZUKFGZIUMUFBJKZFGZCHZUPURCHZFGZIUSUG
    CJKZHZUMVCHZURVCHZFGZIABCJJJAUMIZUIUOULUQVHUHUNCAUMBFLMVHUJUPUKFAUMCNOPBURI
    ZUOUTUQVBVIUNUSCBURUMFQMVIUKVAUPFBURCNRPCVCIZUTVDVBVGCVCUSSVJUPVEVAVFFCVCUM
    SCVCURSTPUMURVCDDAJUAUBDDBJUAUBDDCJUAUBUCUD $.
    $( [23-Aug-2006] $)

  ${
    $d x T $.
    hoaddid1.1 $e |- T : ~H --> ~H $.
    $( Sum of a Hilbert space operator with the zero operator. $)
    hoaddid1i $p |- ( T +op 0hop ) = T $=
      ( vx cv ch0o chos co cfv wceq chil wral wcel cva c0v wf ho0f hosval
      mp3an12 ho0val opreq2d ffvelrni ax-hvaddid syl 3eqtrd rgen hoaddcli
      hoeqi mpbi ) CDZAEFGZHZUIAHZIZCJKUJAIUMCJUIJLZUKULUIEHZMGZULNMGZULJJAOJJE
      OUNUKUPIBPUIAEQRUNUONULMUISTUNULJLUQULIJJUIABUAULUBUCUDUECUJAAEBPUFBUGUH
      $.
      $( [13-Aug-2006] $) $( [15-Nov-2000] $)

    $( Difference of a Hilbert space operator from itself. $)
    hodidi $p |- ( T -op T ) = 0hop $=
      ( chod co ch0o wceq chos hoaddid1i ho0f hodsi mpbir ) AACDEFAEGDAFABHAAEB
      BIJK $.
      $( [30-Jul-2007] $) $( [10-Mar-2006] $)

    $( Composition of the zero operator and a Hilbert space operator. $)
    ho0coi $p |- ( 0hop o. T ) = 0hop $=
      ( vx cv ch0o ccom cfv wceq chil wral wcel c0v ffvelrni ho0val syl ho0f
      hocoi 3eqtr4d rgen hocofi hoeqi mpbi ) CDZEAFZGZUCEGZHZCIJUDEHUGCIUCIKZUC
      AGZEGZLUEUFUHUIIKUJLHIIUCABMUINOUCEAPBQUCNRSCUDEEAPBTPUAUB $.
      $( [9-Aug-2006] $)

    $( Composition of Hilbert space operator with unit identity. $)
    hoid1i $p |- ( T o. Iop ) = T $=
      ( vx chio ccom chil cpj cfv df-iop coeq2i cv wceq wral wcel helch pjfi
      hocoi pjch1 fveq2d eqtrd rgen hocofi hoeqi mpbi eqtri ) ADEAFGHZEZADUFAIJ
      CKZUGHZUHAHZLZCFMUGALUKCFUHFNZUIUHUFHZAHUJUHAUFBFOPZQULUMUHAUHRSTUACUGAAU
      FBUNUBBUCUDUE $.
      $( [18-Sep-2007] $) $( [15-Nov-2000] $)

    $( Composition of Hilbert space operator with unit identity. $)
    hoid1ri $p |- ( Iop o. T ) = T $=
      ( vx chio ccom chil cpj cfv df-iop coeq1i cv wceq wral wcel helch pjfi
      hocoi ffvelrni pjch1 syl eqtrd rgen hocofi hoeqi mpbi eqtri ) DAEFGHZAEZA
      DUGAIJCKZUHHZUIAHZLZCFMUHALULCFUIFNZUJUKUGHZUKUIUGAFOPZBQUMUKFNUNUKLFFUIA
      BRUKSTUAUBCUHAUGAUOBUCBUDUEUF $.
      $( [17-Nov-2007] $) $( [15-Nov-2000] $)
  $}

  $( Sum of a Hilbert space operator with the zero operator. $)
  hoaddid1 $p |- ( T : ~H --> ~H -> ( T +op 0hop ) = T ) $=
    ( chil wf ch0o chos co wceq cif opreq1 id eqeq12d ho0f elimf hoaddid1i
    dedth ) BBACZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRBBADLMNO $.
    $( [31-Jul-2007] $) $( [25-Jul-2006] $)

  $( Difference of a Hilbert space operator from itself. $)
  hodid $p |- ( T : ~H --> ~H -> ( T -op T ) = 0hop ) $=
    ( chil wf chod co ch0o wceq cif id opreq12d eqeq1d ho0f elimf hodidi dedth
    ) BBACZAADEZFGPAFHZRDEZFGAFARGZQSFTARARDTIZUAJKRBBAFLMNO $.
    $( [31-Jul-2007] $) $( [23-Jul-2006] $)

  $( A Hilbert space operator is not empty. $)
  hon0 $p |- ( T : ~H --> ~H -> -. T = (/) ) $=
    ( chil wf c0 wceq c0v wcel wn ax-hv0cl n0i ax-mp wfn fn0 wi ffn fndmu ex
    syl syl5bir mtoi ) BBACZADEZBDEZFBGUCHIBFJKUBADLZUAUCAMUAABLZUDUCNBBAOUEUDU
    CBDAPQRST $.
    $( [24-Mar-2006] $)

  ${
    $d x S $.  $d x T $.
    hodseq.2 $e |- S : ~H --> ~H $.
    hodseq.3 $e |- T : ~H --> ~H $.
    $( Subtraction and addition of equal Hilbert space operators. $)
    hodseqi $p |- ( S +op ( T -op S ) ) = T $=
      ( chod co wceq chos eqid hosubcli hodsi mpbi ) BAEFZMGAMHFBGMIBAMDCBADCJK
      L $.
      $( [27-Aug-2004] $)

    $( Subtraction of Hilbert space operators expressed in terms of difference
       from zero. $)
    ho0subi $p |- ( S -op T ) = ( S +op ( 0hop -op T ) ) $=
      ( chod co ch0o chos wceq ho0f hosubcli hoadd12i hodseqi opreq2i
      hoaddid1i 3eqtri hoaddcli hodsi mpbir ) ABEFAGBEFZHFZIBUAHFZAIUBABTHFZHFA
      GHFABATDCGBJDKZLUCGAHBGDJMNACOPABUACDATCUDQRS $.
      $( [10-Mar-2006] $)

    $( Relationship between Hilbert operator addition and subtraction. $)
    honegsubi $p |- ( S +op ( -u 1 .op T ) ) = ( S -op T ) $=
      ( vx cv c1 cneg chot co chos cfv chod wceq chil wral wcel cmv cva csm
      ffvelrni hvsubval syl11anc cc wf ax-1cn negcli homval mp3an12 opreq2d
      eqtr4d hodval homulcl mp2an hosval 3eqtr4rd rgen hoaddcli hosubcli hoeqi
      mpbi ) EFZAGHZBIJZKJZLZVBABMJZLZNZEOPVEVGNVIEOVBOQZVBALZVBBLZRJZVKVBVDLZS
      JZVHVFVJVMVKVCVLTJZSJZVOVJVKOQVLOQVMVQNOOVBACUAOOVBBDUAVKVLUBUCVJVNVPVKSV
      CUDQZOOBUEZVJVNVPNGUFUGZDVCVBBUHUIUJUKOOAUEZVSVJVHVMNCDVBABULUIWAOOVDUEZV
      JVFVONCVRVSWBVTDVCBUMUNZVBAVDUOUIUPUQEVEVGAVDCWCURABCDUSUTVA $.
      $( [24-Aug-2006] $)

$(
    @( Distributive law for Hilbert space operators. @)
    hosdi @p |- ( A e. CC ->
                 ( A .op ( S +op T ) ) = ( ( A .op S ) +op ( A .op T ) ) ) @=
?@.

    @( Distributive law for Hilbert space operators. @)
    hoddii @p |- ( A e. CC ->
                 ( A .op ( S -op T ) ) = ( ( A .op S ) -op ( A .op T ) ) ) @=
?@.
$)
  $}

  $( Subtraction of Hilbert space operators expressed in terms of difference
     from zero. $)
  ho0sub $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( S -op T ) = ( S +op ( 0hop -op T ) ) ) $=
    ( chil wf chod co ch0o chos wceq cif opreq1 eqeq12d opreq2 opreq2d ho0f
    elimf ho0subi dedth2h ) CCADZCCBDZABEFZAGBEFZHFZISAGJZBEFZUDUBHFZIUDTBGJZEF
    ZUDGUGEFZHFZIABGGAUDIUAUEUCUFAUDBEKAUDUBHKLBUGIZUEUHUFUJBUGUDEMUKUBUIUDHBUG
    GEMNLUDUGCCAGOPCCBGOPQR $.
    $( [25-Jul-2006] $)

  $( The zero operator subtracted from a Hilbert space operator. $)
  hosubid1 $p |- ( T : ~H --> ~H -> ( T -op 0hop ) = T ) $=
    ( chil wf ch0o chod co chos wceq ho0f ho0sub mpan2 hodidi opreq2i a1i
    hoaddid1 3eqtrd ) BBACZADEFZADDEFZGFZADGFZAQBBDCRTHIADJKTUAHQSDAGDILMNAOP
    $.
    $( [25-Jul-2006] $)

  $( Relationship between Hilbert space operator addition and subtraction. $)
  honegsub $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                ( T +op ( -u 1 .op U ) ) = ( T -op U ) ) $=
    ( chil wf c1 cneg chot co chos chod wceq ch0o cif opreq1 eqeq12d opreq2
    opreq2d ho0f elimf honegsubi dedth2h ) CCADZCCBDZAEFZBGHZIHZABJHZKUBALMZUEI
    HZUHBJHZKUHUDUCBLMZGHZIHZUHUKJHZKABLLAUHKUFUIUGUJAUHUEINAUHBJNOBUKKZUIUMUJU
    NUOUEULUHIBUKUDGPQBUKUHJPOUHUKCCALRSCCBLRSTUA $.
    $( [24-Aug-2006] $)

  ${
    $d x A $.  $d x B $.  $d x T $.  $d x U $.
    $( An operator equals its scalar product with one. $)
    homulid2 $p |- ( T : ~H --> ~H -> ( 1 .op T ) = T ) $=
      ( vx chil wf cv c1 chot co cfv wceq wral wcel wa csm cc ax-1cn homval
      mp3an1 ffvelrn ax-hvmulid syl eqtrd r19.21aiva wb homulcl mpan hoeq
      mpancom mpbid ) CCADZBEZFAGHZIZUKAIZJZBCKZULAJZUJUOBCUJUKCLZMZUMFUNNHZUNF
      OLZUJURUMUTJPFUKAQRUSUNCLUTUNJCCUKASUNTUAUBUCCCULDZUJUPUQUDVAUJVBPFAUEUFB
      ULAUGUHUI $.
      $( [12-Aug-2006] $)

    $( Associative law for scalar product and composition of operators. $)
    homco1 $p |- ( ( A e. CC /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( ( A .op T ) o. U ) = ( A .op ( T o. U ) ) ) $=
      ( vx cc wcel chil wf w3a cv chot co ccom cfv wceq wral wa csm ffvelrn
      homval syl3an3 3expa exp43 3imp1 wi wfun homulcl ffun syl fvco3 syl3an1
      3expia 3impa imp opreq2d 3adantl1 3eqtr4d fco syl3an2 3impb eqtr4d
      r19.21aiva wb sylan sylan2 hoeq syl11anc mpbid ) AEFZGGBHZGGCHZIZDJZABKLZ
      CMZNZVMABCMZKLZNZOZDGPZVOVROZVLVTDGVLVMGFZQZVPAVMVQNZRLZVSWDVMCNZVNNZAWGB
      NZRLZVPWFVIVJVKWCWHWJOZVIVJVKWCWKVIVJVKWCQZWKWLVIVJWGGFWKGGVMCSAWGBTUAUBU
      CUDVLWCVPWHOZVIVJVKWCWMUEVIVJQZVKWCWMWNVNUFZVKWCWMWNGGVNHZWOABUGZGGVNUHUI
      GGVMVNCUJUKULUMUNVJVKWCWFWJOVIVJVKQZWCQWEWIARVJVKWCWEWIOZVJBUFVKWCWSGGBUH
      GGVMBCUJUKUBUOUPUQVLWCVSWFOZVIVJVKWCWTUEVIWRWCWTWRVIGGVQHZWCWTGGGBCURZAVM
      VQTUSULUTUNVAVBVLGGVOHZGGVRHZWAWBVCVIVJVKXCWNWPVKXCWQGGGVNCURVDUMVIVJVKXD
      WRVIXAXDXBAVQUGVEUTDVOVRVFVGVH $.
      $( [13-Aug-2006] $)

    $( Scalar product associative law for Hilbert space operators. $)
    homulass $p |- ( ( A e. CC /\ B e. CC /\ T : ~H --> ~H ) ->
                  ( ( A x. B ) .op T ) = ( A .op ( B .op T ) ) ) $=
      ( vx cc wcel chil wf w3a cv cmul co chot cfv wceq wral wa csm ffvelrn
      ax-hvmulass syl3an3 3expa exp43 3imp1 wi mulcl homval syl3an1 3expia
      3impa imp opreq2d 3adantl1 3eqtr4d homulcl syl3an2 3impb eqtr4d
      r19.21aiva wb sylan sylan2 hoeq syl11anc mpbid ) AEFZBEFZGGCHZIZDJZABKLZC
      MLZNZVJABCMLZMLZNZOZDGPZVLVOOZVIVQDGVIVJGFZQZVMAVJVNNZRLZVPWAVKVJCNZRLZAB
      WDRLZRLZVMWCVFVGVHVTWEWGOZVFVGVHVTWHVFVGVHVTQZWHWIVFVGWDGFWHGGVJCSABWDTUA
      UBUCUDVIVTVMWEOZVFVGVHVTWJUEVFVGQZVHVTWJWKVKEFZVHVTWJABUFZVKVJCUGUHUIUJUK
      VGVHVTWCWGOZVFVGVHVTWNVGVHVTIWBWFARBVJCUGULUBUMUNVIVTVPWCOZVFVGVHVTWOUEVF
      VGVHQZVTWOWPVFGGVNHZVTWOBCUOZAVJVNUGUPUIUQUKURUSVIGGVLHZGGVOHZVRVSUTVFVGV
      HWSWKWLVHWSWMVKCUOVAUJVFVGVHWTWPVFWQWTWRAVNUOVBUQDVLVOVCVDVE $.
      $( [12-Aug-2006] $)

    $( Scalar product distributive law for Hilbert space operators. $)
    hoadddi $p |- ( ( A e. CC /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                 ( A .op ( T +op U ) ) = ( ( A .op T ) +op ( A .op U ) ) ) $=
      ( vx cc wcel chil wf w3a cv chos co chot cfv wceq wral wa cva csm
      hoaddcl anim2i 3impb homval 3expa sylan hosval opreq2d 3adantl1 simp1
      adantr ffvelrn 3ad2antl2 3ad2antl3 ax-hvdistr1 syl111anc 3adantl3
      3adantl2 opreq12d eqtr4d 3eqtrd homulcl anim12i 3impdi r19.21aiva wb
      sylan2 syl2an hoeq syl11anc mpbid ) AEFZGGBHZGGCHZIZDJZABCKLZMLZNZVOABMLZ
      ACMLZKLZNZOZDGPZVQWAOZVNWCDGVNVOGFZQZVRVOVSNZVOVTNZRLZWBWGVRAVOVPNZSLZAVO
      BNZVOCNZRLZSLZWJVNVKGGVPHZQZWFVRWLOZVKVLVMWRVLVMQZWQVKBCTZUAUBVKWQWFWSAVO
      VPUCUDUEVLVMWFWLWPOZVKVLVMWFXBVLVMWFIWKWOASVOBCUFUGUDUHWGWPAWMSLZAWNSLZRL
      ZWJWGVKWMGFZWNGFZWPXEOVNVKWFVKVLVMUIUJVLVKWFXFVMGGVOBUKULVMVKWFXGVLGGVOCU
      KUMAWMWNUNUOWGWHXCWIXDRVKVLWFWHXCOZVMVKVLWFXHAVOBUCUDUPVKVMWFWIXDOZVLVKVM
      WFXIAVOCUCUDUQURUSUTVNGGVSHZGGVTHZQZWFWBWJOZVKVLVMXLVKVLQZXJVKVMQZXKABVAZ
      ACVAZVBVCXJXKWFXMVOVSVTUFUDUEUSVDVNGGVQHZGGWAHZWDWEVEVKVLVMXRWTVKWQXRXAAV
      PVAVFUBVKVLVMXSXNXJXKXSXOXPXQVSVTTVGVCDVQWAVHVIVJ $.
      $( [12-Aug-2006] $)

    $( Scalar product reverse distributive law for Hilbert space operators. $)
    hoadddir $p |- ( ( A e. CC /\ B e. CC /\ T : ~H --> ~H ) ->
                 ( ( A + B ) .op T ) = ( ( A .op T ) +op ( B .op T ) ) ) $=
      ( vx cc wcel chil wf w3a cv caddc co chot cfv chos wceq wral wa cva csm
      ffvelrn ax-hvdistr2 syl3an3 3exp exp4a 3imp1 addcl anim1i 3impa homval
      3expa sylan 3adantl2 3adantl1 opreq12d 3eqtr4d homulcl anim12i 3impdir
      hosval eqtr4d r19.21aiva wb hoaddcl syl2an hoeq syl11anc mpbid ) AEFZBEFZ
      GGCHZIZDJZABKLZCMLZNZVMACMLZBCMLZOLZNZPZDGQZVOVSPZVLWADGVLVMGFZRZVPVMVQNZ
      VMVRNZSLZVTWEVNVMCNZTLZAWITLZBWITLZSLZVPWHVIVJVKWDWJWMPZVIVJVKWDWNVIVJVKW
      DRZWNWOVIVJWIGFWNGGVMCUAABWIUBUCUDUEUFVLVNEFZVKRZWDVPWJPZVIVJVKWQVIVJRZWP
      VKABUGZUHUIWPVKWDWRVNVMCUJUKULWEWFWKWGWLSVIVKWDWFWKPZVJVIVKWDXAAVMCUJUKUM
      VJVKWDWGWLPZVIVJVKWDXBBVMCUJUKUNUOUPVLGGVQHZGGVRHZRZWDVTWHPZVIVKVJXEVIVKR
      ZXCVJVKRZXDACUQZBCUQZURUSXCXDWDXFVMVQVRUTUKULVAVBVLGGVOHZGGVSHZWBWCVCVIVJ
      VKXKWSWPVKXKWTVNCUQULUIVIVKVJXLXGXCXDXLXHXIXJVQVRVDVEUSDVOVSVFVGVH $.
      $( [25-Aug-2006] $)
  $}

  $( Swap first and second factors in a nested operator scalar product. $)
  homul12 $p |- ( ( A e. CC /\ B e. CC /\ T : ~H --> ~H ) ->
                ( A .op ( B .op T ) ) = ( B .op ( A .op T ) ) ) $=
    ( cc wcel chil wf w3a cmul co chot wceq wa mulcom opreq1d 3adant3 homulass
    3com12 3eqtr3d ) ADEZBDEZFFCGZHABIJZCKJZBAIJZCKJZABCKJKJBACKJKJZTUAUDUFLUBT
    UAMUCUECKABNOPABCQUATUBUFUGLBACQRS $.
    $( [12-Aug-2006] $)

  $( Double negative of a Hilbert space operator. $)
  honegneg $p |- ( T : ~H --> ~H -> ( -u 1 .op ( -u 1 .op T ) ) = T ) $=
    ( chil wf c1 cneg cmul co chot wceq ax-1cn mul2negi mulid1i eqtri opreq1i
    a1i cc wcel negcli homulass mp3an12 homulid2 3eqtr3d ) BBACZDEZUDFGZAHGZDAH
    GZUDUDAHGHGZAUFUGIUCUEDAHUEDDFGDDDJJKDJLMNOUDPQZUIUCUFUHIDJRZUJUDUDASTAUAUB
    $.
    $( [24-Aug-2006] $)

  $( Relationship between operator subtraction and negative. $)
  hosubneg $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( T -op ( -u 1 .op U ) ) = ( T +op U ) ) $=
    ( chil wf wa c1 cneg chot co chos chod wceq cc wcel ax-1cn negcli homulcl
    mpan honegsub sylan2 honegneg opreq2d adantl eqtr3d ) CCADZCCBDZEAFGZUGBHIZ
    HIZJIZAUHKIZABJIZUFUECCUHDZUJUKLUGMNUFUMFOPUGBQRAUHSTUFUJULLUEUFUIBAJBUAUBU
    CUD $.
    $( [25-Aug-2006] $)

  $( Scalar product distributive law for operator difference. $)
  hosubdi $p |- ( ( A e. CC /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
               ( A .op ( T -op U ) ) = ( ( A .op T ) -op ( A .op U ) ) ) $=
    ( cc wcel chil wf w3a c1 cneg chot co chos chod wceq ax-1cn negcli homulcl
    mpan hoadddi syl3an3 homul12 mp3an2 3adant2 opreq2d eqtrd wa honegsub
    3adant1 syl2an 3impdi 3eqtr3d ) ADEZFFBGZFFCGZHZABIJZCKLZMLZKLZABKLZUQACKLZ
    KLZMLZABCNLZKLZVAVBNLZUPUTVAAURKLZMLZVDUOUMUNFFURGZUTVIOUQDEZUOVJIPQZUQCRSA
    BURTUAUPVHVCVAMUMUOVHVCOZUNUMVKUOVMVLAUQCUBUCUDUEUFUNUOUTVFOUMUNUOUGUSVEAKB
    CUHUEUIUMUNUOVDVGOZUMUNUGFFVAGFFVBGVNUMUOUGABRACRVAVBUHUJUKUL $.
    $( [12-Aug-2006] $)

  $( Distribution of negative over addition. $)
  honegdi $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
          ( -u 1 .op ( T +op U ) ) = ( ( -u 1 .op T ) +op ( -u 1 .op U ) ) ) $=
    ( c1 cneg cc wcel chil wf chos co chot wceq ax-1cn negcli hoadddi mp3an1 )
    CDZEFGGAHGGBHQABIJKJQAKJQBKJIJLCMNQABOP $.
    $( [24-Aug-2006] $)

  $( Distribution of negative over subtraction. $)
  honegsubdi $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                    ( -u 1 .op ( T -op U ) ) = ( ( -u 1 .op T ) +op U ) ) $=
    ( chil wf wa c1 cneg chot co chos chod wceq cc wcel ax-1cn negcli homulcl
    mpan honegdi sylan2 honegsub opreq2d honegneg adantl 3eqtr3d ) CCADZCCBDZEZ
    FGZAUIBHIZJIZHIZUIAHIZUIUJHIZJIZUIABKIZHIUMBJIUGUFCCUJDZULUOLUIMNUGUQFOPUIB
    QRAUJSTUHUKUPUIHABUAUBUHUNBUMJUGUNBLUFBUCUDUBUE $.
    $( [24-Aug-2006] $)

  $( Distribution of negative over subtraction. $)
  honegsubdi2 $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                     ( -u 1 .op ( T -op U ) ) = ( U -op T ) ) $=
    ( chil wf wa c1 cneg chod co chot chos honegsubdi wceq cc wcel ax-1cn
    negcli homulcl mpan hoaddcom sylan honegsub ancoms 3eqtrd ) CCADZCCBDZEFGZA
    BHIJIUGAJIZBKIZBUHKIZBAHIZABLUECCUHDZUFUIUJMUGNOUEULFPQUGARSUHBTUAUFUEUJUKM
    BAUBUCUD $.
    $( [24-Aug-2006] $)

  $( Law for double subtraction of Hilbert space operators. $)
  hosubsub2 $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( S -op ( T -op U ) ) = ( S +op ( U -op T ) ) ) $=
    ( chil wf w3a c1 cneg chod co chot chos wceq wa hosubcl honegsub sylan2
    3impb honegsubdi2 opreq2d 3adant1 eqtr3d ) DDAEZDDBEZDDCEZFAGHBCIJZKJZLJZAU
    FIJZACBIJZLJZUCUDUEUHUIMZUDUENZUCDDUFEULBCOAUFPQRUDUEUHUKMUCUMUGUJALBCSTUAU
    B $.
    $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a mixed addition and subtraction of Hilbert
     space operators. $)
  hosub4 $p |- ( ( ( R : ~H --> ~H /\ S : ~H --> ~H ) /\
      ( T : ~H --> ~H /\ U : ~H --> ~H ) ) -> ( ( R +op S ) -op ( T +op U ) ) =
                ( ( R -op T ) +op ( S -op U ) ) ) $=
    ( chil wf wa chos co c1 cneg chot chod wceq honegdi adantl opreq2d cc wcel
    ax-1cn negcli homulcl mpan anim12i hoadd4 sylan2 eqtrd hoaddcl honegsub
    syl2an ad2ant2r ad2ant2l opreq12d 3eqtr3d ) EEAFZEEBFZGZEECFZEEDFZGZGZABHIZ
    JKZCDHIZLIZHIZAVCCLIZHIZBVCDLIZHIZHIZVBVDMIZACMIZBDMIZHIVAVFVBVGVIHIZHIZVKV
    AVEVOVBHUTVEVONUQCDOPQUTUQEEVGFZEEVIFZGVPVKNURVQUSVRVCRSZURVQJTUAZVCCUBUCVS
    USVRVTVCDUBUCUDABVGVIUEUFUGUQEEVBFEEVDFVFVLNUTABUHCDUHVBVDUIUJVAVHVMVJVNHUO
    URVHVMNUPUSACUIUKUPUSVJVNNUOURBDUIULUMUN $.
    $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a mixed addition and subtraction of Hilbert
     space operators. $)
  hosubadd4 $p |- ( ( ( R : ~H --> ~H /\ S : ~H --> ~H ) /\
      ( T : ~H --> ~H /\ U : ~H --> ~H ) ) -> ( ( R -op S ) -op ( T -op U ) ) =
                ( ( R +op U ) -op ( S +op T ) ) ) $=
    ( chil wf wa chod co chos wceq hosubcl hosubsub2 3expb sylan hosub4 an42s
    eqtr4d ) EEAFZEEBFZGZEECFZEEDFZGZGABHIZCDHIHIZUEDCHIJIZADJIBCJIHIZUAEEUEFZU
    DUFUGKZABLUIUBUCUJUECDMNOSUCTUBUHUGKADBCPQR $.
    $( [24-Aug-2006] $)

  $( Associative-type law for addition and subtraction of Hilbert space
     operators. $)
  hoaddsubass $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( ( S +op T ) -op U ) = ( S +op ( T -op U ) ) ) $=
    ( chil wf w3a chos co ch0o chod wceq ho0f hosubcl mpan hoaddass syl3an3 wa
    hoaddcl ho0sub sylan 3impa 3adant1 opreq2d 3eqtr4d ) DDAEZDDBEZDDCEZFZABGHZ
    ICJHZGHZABUJGHZGHZUICJHZABCJHZGHUGUEUFDDUJEZUKUMKDDIEUGUPLICMNABUJOPUEUFUGU
    NUKKZUEUFQDDUIEUGUQABRUICSTUAUHUOULAGUFUGUOULKUEBCSUBUCUD $.
    $( [25-Aug-2006] $)

  $( Law for operator addition and subtraction of Hilbert space operators. $)
  hoaddsub $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( ( S +op T ) -op U ) = ( ( S -op U ) +op T ) ) $=
    ( chil wf w3a chos co chod wceq wa hoaddcom opreq1d 3adant3 hoaddsubass
    3com12 wi hosubcl ex syl5 exp3a com12 3imp 3eqtrd ) DDAEZDDBEZDDCEZFABGHZCI
    HZBAGHZCIHZBACIHZGHZULBGHZUEUFUIUKJUGUEUFKUHUJCIABLMNUFUEUGUKUMJBACOPUEUFUG
    UMUNJZUFUEUGUOQUFUEUGUOUEUGKDDULEZUFUOACRUFUPUOBULLSTUAUBUCUD $.
    $( [25-Aug-2006] $)

  $( Law for double subtraction of Hilbert space operators. $)
  hosubsub $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( S -op ( T -op U ) ) = ( ( S -op T ) +op U ) ) $=
    ( chil wf w3a chod co chos hosubsub2 wceq hoaddsubass hoaddsub eqtr3d
    3com23 eqtrd ) DDAEZDDBEZDDCEZFABCGHGHACBGHIHZABGHCIHZABCJQSRTUAKQSRFACIHBG
    HTUAACBLACBMNOP $.
    $( [25-Aug-2006] $)

  $( Law for double subtraction of Hilbert space operators. $)
  hosubsub4 $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( ( S -op T ) -op U ) = ( S -op ( T +op U ) ) ) $=
    ( chil wf w3a c1 cneg chot co chod chos wceq cc wcel ax-1cn negcli homulcl
    mpan hosubsub syl3an3 hosubneg 3adant1 opreq2d wa hosubcl honegsub sylan
    3impa 3eqtr3rd ) DDAEZDDBEZDDCEZFZABGHZCIJZKJZKJZABKJZUPLJZABCLJZKJUSCKJZUM
    UKULDDUPEZURUTMUONOUMVCGPQUOCRSABUPTUAUNUQVAAKULUMUQVAMUKBCUBUCUDUKULUMUTVB
    MZUKULUEDDUSEUMVDABUFUSCUGUHUIUJ $.
    $( [25-Aug-2006] $)

  $( Two times a Hilbert space operator. $)
  ho2times $p |- ( T : ~H --> ~H -> ( 2 .op T ) = ( T +op T ) ) $=
    ( chil wf c2 chot co c1 chos caddc df-2 opreq1i cc wcel wceq ax-1cn
    hoadddir mp3an12 syl5eq hoadddi mp3an1 anidms hoaddcl homulid2 syl 3eqtr2d
    ) BBACZDAEFZGAEFZUHHFZGAAHFZEFZUJUFUGGGIFZAEFZUIDULAEJKGLMZUNUFUMUINOOGGAPQ
    RUFUKUINZUNUFUFUOOGAASTUAUFBBUJCZUKUJNUFUPAAUBUAUJUCUDUE $.
    $( [26-Aug-2006] $)

  ${
    hoaddsubass.1 $e |- R : ~H --> ~H $.
    hoaddsubass.2 $e |- S : ~H --> ~H $.
    hoaddsubass.3 $e |- T : ~H --> ~H $.
    $( Associativity of sum and difference of Hilbert space operators. $)
    hoaddsubassi $p |- ( ( R +op S ) -op T ) = ( R +op ( S -op T ) ) $=
      ( chil wf chos co chod wceq hoaddsubass mp3an ) GGAHGGBHGGCHABIJCKJABCKJI
      JLDEFABCMN $.
      $( [27-Aug-2004] $)

    $( Law for sum and difference of Hilbert space operators. $)
    hoaddsubi $p |- ( ( R +op S ) -op T ) = ( ( R -op T ) +op S ) $=
      ( chos co chod hoaddcomi opreq1i hoaddsubassi hosubcli 3eqtri ) ABGHZCIHB
      AGHZCIHBACIHZGHQBGHOPCIABDEJKBACEDFLBQEACDFMJN $.
      $( [27-Aug-2004] $)
  $}

  ${
    hosd1.2 $e |- T : ~H --> ~H $.
    hosd1.3 $e |- U : ~H --> ~H $.
    $( Hilbert space operator sum expressed in terms of difference. $)
    hosd1i $p |- ( T +op U ) = ( T -op ( 0hop -op U ) ) $=
      ( ch0o chod co chos wceq ho0f hosubcli hoaddcli hoaddcomi hoaddsubassi
      hoaddid1i opreq1i hoaddsubi hodseqi eqtri 3eqtri 3eqtr2i hodsi mpbir
      eqcomi ) AEBFGZFGZABHGZUFUGIUEUGHGZAIUHUGUEHGUGEHGZBFGZAUEUGEBJDKZABCDLZM
      UGEBULJDNUJUGBFGABFGZBHGZAUIUGBFUGULOPABBCDDQUNBUMHGAUMBABCDKDMBADCRSTUAA
      UEUGCUKULUBUCUD $.
      $( [3-Aug-2007] $) $( [27-Aug-2004] $)

    $( Hilbert space operator sum expressed in terms of difference. $)
    hosd2i $p |- ( T +op U ) = ( T -op ( ( U -op U ) -op U ) ) $=
      ( chos co ch0o chod hosd1i hodidi opreq1i opreq2i eqtr4i ) ABEFAGBHFZHFAB
      BHFZBHFZHFABCDIPNAHOGBHBDJKLM $.
      $( [27-Aug-2004] $)

    $( Hilbert space operator cancellation law. $)
    hopncani $p |- ( ( T +op U ) -op U ) = T $=
      ( chos co chod ch0o hoaddsubassi hodidi opreq2i hoaddid1i 3eqtri ) ABEFBG
      FABBGFZEFAHEFAABBCDDINHAEBDJKACLM $.
      $( [10-Mar-2006] $)

    $( Hilbert space operator cancellation law. $)
    honpcani $p |- ( ( T -op U ) +op U ) = T $=
      ( chos co chod hoaddsubi hopncani eqtr3i ) ABEFBGFABGFBEFAABBCDDHABCDIJ
      $.
      $( [11-Mar-2006] $)

    $( If the difference between two operators is zero, they are equal. $)
    hosubeq0i $p |- ( ( T -op U ) = 0hop <-> T = U ) $=
      ( chod co ch0o wceq c1 cneg chot chos honegsubi eqeq1i opreq1 sylbir cc
      wcel chil wf ax-1cn negcli homulcl mp2an hoadd23i hoaddassi hodidi eqtri
      opreq2i hoaddid1i 3eqtri ho0f hoaddcomi 3eqtr3g syl6eq impbii ) ABEFZGHZA
      BHZURAIJZBKFZLFZBLFZGBLFZABURVBGHVCVDHVBUQGABCDMNVBGBLOPVCABLFVALFABVALFZ
      LFZAAVABCUTQRSSBTSSVATIUAUBDUTBUCUDZDUEABVACDVGUFVFAGLFAVEGALVEBBEFZGBBDD
      MBDUGZUHUIACUJUHUKVDBGLFBGBULDUMBDUJUHUNUSUQVHGABBEOVIUOUP $.
      $( [31-Jul-2007] $) $( [27-Jul-2006] $)
  $}

  ${
    honpncan.1 $e |- R : ~H --> ~H $.
    honpncan.2 $e |- S : ~H --> ~H $.
    honpncan.3 $e |- T : ~H --> ~H $.
    $( Hilbert space operator cancellation law. $)
    honpncani $p |- ( ( R -op S ) +op ( S -op T ) ) = ( R -op T ) $=
      ( chod co chos hosubcli hoaddsubassi honpcani opreq1i eqtr3i ) ABGHZBIHZC
      GHOBCGHIHACGHOBCABDEJEFKPACGABDELMN $.
      $( [11-Mar-2006] $)
  $}

  ${
    $d x y T $.
    ho0.1 $e |- T : ~H --> ~H $.
    $( A condition implying that a Hilbert space operator is identically zero.
       Lemma 3.2(S8) of [Beran] p. 95. $)
    ho01i $p |- ( A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = 0 <->
               T = 0hop ) $=
      ( chil c0v csn cxp wceq cv cfv wral ch0o csp co cc0 wfn wb wf ffn ax-mp
      ax-hv0cl elisseti fconst eqfnfv2 mp2an c0h df0op2 df-ch0 xpeq2i eqtri
      eqeq2i wcel ffvelrni hial0 syl fvconst2 eqeq2d bitr4d ralbiia 3bitr4ri )
      CEFGZHZIZAJZCKZVEVCKZIZAELZCMIVFBJNOPIBELZAELCEQZVCEQZVDVIREECSVKDEECTUAE
      VBVCSVLEFFEUBUCZUDEVBVCTUAAECVCUEUFMVCCMEUGHVCUHUGVBEUIUJUKULVJVHAEVEEUMZ
      VJVFFIZVHVNVFEUMVJVOREEVECDUNBVFUOUPVNVGFVFEFVEVMUQURUSUTVA $.
      $( [31-Jul-2007] $) $( [28-Jan-2006] $)

    $( A condition implying that a Hilbert space operator is identically zero.
       Lemma 3.2(S10) of [Beran] p. 95. $)
    ho02i $p |- ( A. x e. ~H A. y e. ~H ( x .ih ( T ` y ) ) = 0 <->
               T = 0hop ) $=
      ( cv cfv csp co cc0 wceq chil wral ch0o ralcom wcel wb ffvelrni c0v
      hial02 hial0 bitr4d syl ralbiia ho01i 3bitri ) AEZBEZCFZGHIJZBKLAKLUIAKLZ
      BKLUHUFGHIJAKLZBKLCMJUIABKKNUJUKBKUGKOUHKOZUJUKPKKUGCDQULUJUHRJUKAUHSAUHT
      UAUBUCBACDUDUE $.
      $( [30-Apr-2008] $) $( [28-Jan-2006] $)
  $}

  ${
    $d x y z S $.  $d u v x y z T $.
    $( A condition implying that two Hilbert space operators are equal.  Lemma
       3.2(S9) of [Beran] p. 95. $)
    hoeq1 $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) -> ( A. x e. ~H
          A. y e. ~H ( ( S ` x ) .ih y ) = ( ( T ` x ) .ih y ) <-> S = T ) ) $=
      ( chil wf wa cv cfv csp co wceq wral wcel wb ffvelrn hial2eq syl2an
      anandirs ralbidva wfn ffn eqfnfv2 bitr4d ) EECFZEEDFZGZAHZCIZBHZJKUHDIZUJ
      JKLBEMZAEMUIUKLZAEMZCDLZUGULUMAEUEUFUHENZULUMOZUEUPGUIENUKENUQUFUPGEEUHCP
      EEUHDPBUIUKQRSTUECEUADEUAUOUNOUFEECUBEEDUBAECDUCRUD $.
      $( [15-Feb-2006] $)

    $( A condition implying that two Hilbert space operators are equal.  Lemma
       3.2(S11) of [Beran] p. 95. $)
    hoeq2 $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) -> ( A. x e. ~H
          A. y e. ~H ( x .ih ( S ` y ) ) = ( x .ih ( T ` y ) ) <-> S = T ) ) $=
      ( chil wf wa cv cfv csp co wceq wral wb ralcom a1i wcel ffvelrn hial2eq2
      hial2eq bitr4d syl2an anandirs ralbidva hoeq1 3bitrd ) EECFZEEDFZGZAHZBHZ
      CIZJKUJUKDIZJKLZBEMAEMZUNAEMZBEMZULUJJKUMUJJKLAEMZBEMCDLUOUQNUIUNABEEOPUI
      UPURBEUGUHUKEQZUPURNZUGUSGULEQZUMEQZUTUHUSGEEUKCREEUKDRVAVBGUPULUMLURAULU
      MSAULUMTUAUBUCUDBACDUEUF $.
      $( [15-Feb-2006] $)

    $( Every Hilbert space operator has at most one adjoint. $)
    adjmo $p |- E* u ( u : ~H --> ~H /\
           A. x e. ~H A. y e. ~H ( x .ih ( T ` y ) ) = ( ( u ` x ) .ih y ) ) $=
      ( vv chil cv wf cfv csp co wceq wral wa wmo wi wal r19.26-2 eqtr2 ralimi
      sylbir hoeq1 biimpa sylan2 an4s gen2 feq1 fveq1 opreq1d eqeq2d 2ralbidv
      anbi12d mo4 mpbir ) FFCGZHZAGZBGZDIJKZUQUOIZURJKZLZBFMAFMZNZCOVDFFEGZHZUS
      UQVEIZURJKZLZBFMAFMZNZNUOVELZPZEQCQVMCEUPVFVCVJVLVCVJNZUPVFNZVAVHLZBFMZAF
      MZVLVNVBVINZBFMZAFMVRVBVIABFFRVTVQAFVSVPBFUSVAVHSTTUAVOVRVLABUOVEUBUCUDUE
      UFVDVKCEVLUPVFVCVJFFUOVEUGVLVBVIABFFVLVAVHUSVLUTVGURJUQUOVEUHUIUJUKULUMUN
      $.
      $( [18-Feb-2006] $)

    $( Symmetry property of an adjoint. $)
    adjsym $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
          ( A. x e. ~H A. y e. ~H ( x .ih ( S ` y ) ) = ( ( T ` x ) .ih y ) <->
         A. x e. ~H A. y e. ~H ( x .ih ( T ` y ) ) = ( ( S ` x ) .ih y ) ) ) $=
      ( vz chil wf wa cv cfv csp co wceq wral wcel wb ccj ffvelrn ax-his1
      sylan adantrl sylan2 adantll eqeq12d ancoms cc hicl cj11 syl11anc bitr2d
      an4s anassrs eqcom syl6bb ralbidva ralcom fveq2 opreq2d opreq2 ralbidv
      cbvralv bitr4i opreq1 opreq1d ralbii 3bitri syl6rbbr ) FFCGZFFDGZHZAIZBIZ
      DJZKLZVKCJZVLKLZMZBFNZAFNVLVOKLZVMVKKLZMZBFNZAFNZVKVLCJZKLZVKDJZVLKLZMZBF
      NAFNZVJVRWBAFVJVKFOZHZVQWABFWKVLFOZHVQVTVSMZWAVJWJWLVQWMPZVHWJVIWLWNVHWJH
      ZVIWLHZHZWMVNQJZVPQJZMZVQWPWOWMWTPWPWOHVTWRVSWSWPWJVTWRMZVHWPVMFOZWJXAFFV
      LDRZVMVKSTUAWLWOVSWSMZVIWOWLVOFOZXDFFVKCRZVLVOSUBUCUDUEWQVNUFOZVPUFOZWTVQ
      PWJWPXGVHWPWJXBXGXCVKVMUGUBUCWOWLXHVIWOXEWLXHXFVOVLUGTUAVNVPUHUIUJUKULVTV
      SUMUNUOUOWIVKEIZCJZKLZWFXIKLZMZAFNZEFNZVLXJKLZVMXIKLZMZBFNZEFNWCWIWHAFNZB
      FNXOWHABFFUPXNXTEBFXIVLMZXMWHAFYAXKWEXLWGYAXJWDVKKXIVLCUQURXIVLWFKUSUDUTV
      AVBXNXSEFXMXRABFVKVLMZXKXPXLXQVKVLXJKVCYBWFVMXIKVKVLDUQVDUDVAVEXSWBEAFXIV
      KMZXRWABFYCXPVSXQVTYCXJVOVLKXIVKCUQURXIVKVMKUSUDUTVAVFVG $.
      $( [18-Feb-2006] $)
  $}

  ${
    eigre.1 $e |- A e. ~H $.
    eigre.2 $e |- B e. CC $.
    $( A necessary and sufficient condition (that holds when ` T ` is a
       Hermitian operator) for an eigenvalue ` B ` to be real.  Generalization
       of Equation 1.30 of [Hughes] p. 49. $)
    eigrei $p |- ( ( ( T ` A ) = ( B .h A ) /\ A =/= 0h ) ->
                 ( ( A .ih ( T ` A ) ) = ( ( T ` A ) .ih A ) <-> B e. RR ) ) $=
      ( cfv csm co wceq c0v wne wa csp ccj cr wcel cmul opreq2 cc chil his5
      mp3an syl6eq opreq1 ax-his3 eqeq12d cc0 clt wbr wb ax-his4 mpan hiidrcl
      ax-mp gt0ne0i hicli cjcli mulcan2 mp3an1 3syl sylan9bb cjrebi syl6bbr )
      ACFZBAGHZIZAJKZLAVDMHZVDAMHZIZBNFZBIZBOPVFVJVKAAMHZQHZBVMQHZIZVGVLVFVHVNV
      IVOVFVHAVEMHZVNVDVEAMRBSPZATPZVSVQVNIEDDBAAUAUBUCVFVIVEAMHZVOVDVEAMUDVRVS
      VSVTVOIEDDBAAUEUBUCUFVGUGVMUHUIZVMUGKZVPVLUJZVSVGWADAUKULVMVSVMOPDAUMUNUO
      VMSPZWBWCAADDUPVRWDWBLZWCEVKSPVRWEWCBEUQVKBVMURUSULULUTVABEVBVC $.
      $( [18-May-2006] $) $( [21-Jan-2005] $)
  $}

  $( A necessary and sufficient condition (that holds when ` T ` is a Hermitian
     operator) for an eigenvalue ` B ` to be real.  Generalization of Equation
     1.30 of [Hughes] p. 49. $)
  eigre $p |- ( ( ( A e. ~H /\ B e. CC ) /\ ( ( T ` A ) = ( B .h A ) /\ A =/=
       0h ) ) -> ( ( A .ih ( T ` A ) ) = ( ( T ` A ) .ih A ) <-> B e. RR ) ) $=
    ( chil wcel cc wa cfv csm co wceq c0v wne csp cr wb wi cif cc0 fveq2
    opreq2 eqeq12d neeq1 anbi12d id opreq12d bibi1d imbi12d opreq1 eqeq2d
    anbi1d eleq1 bibi2d ax-hv0cl elimel 0cn eigrei dedth2h imp ) ADEZBFEZGACHZB
    AIJZKZALMZGZAVBNJZVBANJZKZBOEZPZUTVAVFVKQUTALRZCHZBVLIJZKZVLLMZGZVLVMNJZVMV
    LNJZKZVJPZQVMVABSRZVLIJZKZVPGZVTWBOEZPZQABLSAVLKZVFVQVKWAWHVDVOVEVPWHVBVMVC
    VNAVLCTZAVLBIUAUBAVLLUCUDWHVIVTVJWHVGVRVHVSWHAVLVBVMNWHUEZWIUFWHVBVMAVLNWIW
    JUFUBUGUHBWBKZVQWEWAWGWKVOWDVPWKVNWCVMBWBVLIUIUJUKWKVJWFVTBWBOULUMUHVLWBCAL
    DUNUOBSFUPUOUQURUS $.
    $( [19-Mar-2006] $)

  ${
    eigpos.1 $e |- A e. ~H $.
    eigpos.2 $e |- B e. CC $.
    $( A sufficient condition (first conjunct pair, that holds when ` T ` is a
       positive operator) for an eigenvalue ` B ` (second conjunct pair) to be
       nonnegative.  Remark (ii) in [Hughes] p. 137. $)
    eigposi $p |- ( ( ( ( A .ih ( T ` A ) ) e. RR /\ 0 <_ ( A .ih ( T ` A ) ) )
      /\ ( ( T ` A ) = ( B .h A ) /\ A =/= 0h ) ) -> ( B e. RR /\ 0 <_ B ) ) $=
      ( cfv csp co cr wcel cc0 cle wbr wa csm wceq c0v wne wb chil hvmulcli
      hire mp2an a1i opreq2 eleq1d opreq1 eqeq12d 3bitr4d adantr eigrei bitrd
      biimpac adantlr clt cmul ax-his4 mpan ad2antll simplr ad2antrl ccj cc
      his5 mp3an cjrebi sylib opreq1d syl5eq eqtrd breqtrd hiidrcl ax-mp
      prodge02 mpanl2 syl12anc jca ) AACFZGHZIJZKVSLMZNZVRBAOHZPZAQRZNZNZBIJZKB
      LMZVTWFWHWAWFVTWHWFVTVSVRAGHZPZWHWDVTWKSWEWDAWCGHZIJZWLWCAGHZPZVTWKWMWOSZ
      WDATJZWCTJWPDBAEDUAAWCUBUCUDWDVSWLIVRWCAGUEZUFWDVSWLWJWNWRVRWCAGUGUHUIUJA
      BCDEUKULUMUNZWGWHKAAGHZUOMZKBWTUPHZLMZWIWSWEXAWBWDWQWEXADAUQURUSWGKVSXBLV
      TWAWFUTWGVSWLXBWDVSWLPWBWEWRVAWGWLBVBFZWTUPHZXBBVCJWQWQWLXEPEDDBAAVDVEWGX
      DBWTUPWGWHXDBPWSBEVFVGVHVIVJVKWHWTIJZXAXCNWIWQXFDAVLVMBWTVNVOVPVQ $.
      $( [13-Oct-2006] $) $( [2-Jul-2005] $)
  $}

  ${
    eigorthi.1 $e |- A e. ~H $.
    eigorthi.2 $e |- B e. ~H $.
    eigorthi.3 $e |- C e. CC $.
    eigorthi.4 $e |- D e. CC $.
    $( A necessary and sufficient condition (that holds when ` T ` is a
       Hermitian operator) for two eigenvectors ` A ` and ` B ` to be
       orthogonal.  Generalization of Equation 1.31 of [Hughes] p. 49. $)
    eigorthi $p |- ( ( ( ( T ` A ) = ( C .h A ) /\ ( T ` B ) = ( D .h B ) )
                     /\ C =/= ( * ` D ) ) ->
         ( ( A .ih ( T ` B ) ) = ( ( T ` A ) .ih B ) <-> ( A .ih B ) = 0 ) ) $=
      ( cfv csm co wceq wa csp ccj cmul wne cc0 opreq2 cc wcel chil his5 mp3an
      syl6eq opreq1 ax-his3 eqeqan12rd wb hicli cjcli mulcan2 mp3an12 mpan
      eqcom syl6bb biimpcd necon1d com12 mul01i eqtr4i eqtr4d impbid1 sylan9bb
      ) AEJZCAKLZMZBEJZDBKLZMZNAVIOLZVFBOLZMDPJZABOLZQLZCVOQLZMZCVNRZVOSMZVKVHV
      LVPVMVQVKVLAVJOLZVPVIVJAOTDUAUBAUCUBZBUCUBZWAVPMIFGDABUDUEUFVHVMVGBOLZVQV
      FVGBOUGCUAUBZWBWCWDVQMHFGCABUHUEUFUIVSVRVTVRVSVTVRVOSCVNVOSRZVRCVNMZWFVRV
      NCMZWGVOUAUBZWFVRWHUJZABFGUKVNUAUBWEWIWFNWJDIULZHVNCVOUMUNUOVNCUPUQURUSUT
      VTVPVNSQLZVQVOSVNQTVTVQCSQLZWLVOSCQTWMSWLCHVAVNWKVAVBUFVCVDVE $.
      $( [13-Jan-2009] $) $( [23-Jan-2005] $)
  $}

  $( A necessary and sufficient condition (that holds when ` T ` is a Hermitian
     operator) for two eigenvectors ` A ` and ` B ` to be orthogonal.
     Generalization of Equation 1.31 of [Hughes] p. 49. $)
  eigorth $p |- ( ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. CC /\ D e. CC ) ) /\
                 ( ( ( T ` A ) = ( C .h A ) /\ ( T ` B ) = ( D .h B ) )
                   /\ C =/= ( * ` D ) ) ) ->
         ( ( A .ih ( T ` B ) ) = ( ( T ` A ) .ih B ) <-> ( A .ih B ) = 0 ) ) $=
    ( chil wcel wa cc cfv csm co wceq ccj wne csp cc0 wb wi c0v cif fveq2
    opreq2 eqeq12d anbi1d opreq1 opreq1d eqeq1d bibi12d imbi12d anbi2d opreq2d
    eqeq2d neeq1 anbi12d imbi1d neeq2d ax-hv0cl elimel 0cn eigorthi dedth4h
    imp ) AFGZBFGZHCIGZDIGZHHAEJZCAKLZMZBEJZDBKLZMZHZCDNJZOZHZAVKPLZVHBPLZMZABP
    LZQMZRZVDVEVFVGVQWCSVDATUAZEJZCWDKLZMZVMHZVPHZWDVKPLZWEBPLZMZWDBPLZQMZRZSWG
    VEBTUAZEJZDWPKLZMZHZVPHZWDWQPLZWEWPPLZMZWDWPPLZQMZRZSWEVFCQUAZWDKLZMZWSHZXH
    VOOZHZXGSXJWQVGDQUAZWPKLZMZHZXHXNNJZOZHZXGSABCDTTQQAWDMZVQWIWCWOYAVNWHVPYAV
    JWGVMYAVHWEVIWFAWDEUBZAWDCKUCUDUEUEYAVTWLWBWNYAVRWJVSWKAWDVKPUFYAVHWEBPYBUG
    UDYAWAWMQAWDBPUFUHUIUJBWPMZWIXAWOXGYCWHWTVPYCVMWSWGYCVKWQVLWRBWPEUBZBWPDKUC
    UDUKUEYCWLXDWNXFYCWJXBWKXCYCVKWQWDPYDULBWPWEPUCUDYCWMXEQBWPWDPUCUHUIUJCXHMZ
    XAXMXGYEWTXKVPXLYEWGXJWSYEWFXIWECXHWDKUFUMUECXHVOUNUOUPDXNMZXMXTXGYFXKXQXLX
    SYFWSXPXJYFWRXOWQDXNWPKUFUMUKYFVOXRXHDXNNUBUQUOUPWDWPXHXNEATFURUSBTFURUSCQI
    UTUSDQIUTUSVAVBVC $.
    $( [15-Jan-2009] $) $( [23-Mar-2006] $)

