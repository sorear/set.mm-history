$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Operations on Hilbert space operators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A $.  $d x y S $.  $d x y T $.
    $( The sum of Hilbert space operators is an operator. $)
    hoaddclt $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ ) ->
                ( S +op T ) : H~ --> H~ ) $=
      ( vx vy chil wf wa chos co cv wcel cfv cva wceq copab wral hvaddclt
      ffvelrn syl2an anandirs r19.21aiva eqid fopab2 sylib hosmvalt feq1d
      mpbird ) EEAFZEEBFZGZEEABHIZFEECJZEKZDJULALZULBLZMIZNGCDOZFZUJUPEKZCEPURU
      JUSCEUHUIUMUSUNEKUOEKUSUHUMGUIUMGUNUOQEEULAREEULBRSTUACDEEUPUQUQUBUCUDUJE
      EUKUQCDABUEUFUG $.
      $( [22-Feb-2006] $) $( [21-Feb-2006] $)

    $( The scalar product of a Hilbert space operator is an operator. $)
    homulclt $p |- ( ( A e. CC /\ T : H~ --> H~ ) ->
                   ( A .op T ) : H~ --> H~ ) $=
      ( vx vy cc wcel chil wf wa chot co cv cfv csm wceq copab wral hvmulclt
      ffvelrn sylan2 anassrs r19.21aiva eqid fopab2 sylib hommvalt feq1d
      mpbird ) AEFZGGBHZIZGGABJKZHGGCLZGFZDLAUMBMZNKZOICDPZHZUKUPGFZCGQURUKUSCG
      UIUJUNUSUIUOGFUSUJUNIAUORGGUMBSTUAUBCDGGUPUQUQUCUDUEUKGGULUQCDABUFUGUH $.
      $( [22-Mar-2006] $) $( [21-Feb-2006] $)
  $}

  ${
    $d x T $.  $d x U $.
    $( Equality of Hilbert space operators. $)
    hoeqt $p |- ( ( T : H~ --> H~ /\ U : H~ --> H~ ) ->
                ( A. x e. H~ ( T ` x ) = ( U ` x ) <-> T = U ) ) $=
      ( chil wfn cv cfv wceq wral wb wf wa eqfnfv eqid biantrur syl6rbbr ffn
      syl2an ) BDEZCDEZAFZBGUACGHADIZBCHZJDDBKDDCKSTLUCDDHZUBLUBADDBCMUDUBDNOPD
      DBQDDCQR $.
      $( [13-Aug-2006] $) $( [12-Aug-2006] $)
  $}

  ${
    $d x y S $.  $d x y T $.
    hoeq.1 $e |- S : H~ --> H~ $.
    hoeq.2 $e |- T : H~ --> H~ $.
    $( Equality of Hilbert space operators. $)
    hoeq $p |- ( A. x e. H~ ( S ` x ) = ( T ` x ) <-> S = T ) $=
      ( chil wf cv cfv wceq wral wb hoeqt mp2an ) FFBGFFCGAHZBIOCIJAFKBCJLDEABC
      MN $.
      $( [14-Nov-2000] $)

    $( Closure of Hilbert space operator sum. $)
    hoscl $p |- ( A e. H~ -> ( ( S +op T ) ` A ) e. H~ ) $=
      ( chil wf wcel chos co cfv hosclt mpanl12 ) FFBGFFCGAFHABCIJKFHDEABCLM $.
      $( [12-Nov-2000] $)

    $( Closure of Hilbert space operator difference. $)
    hodcl $p |- ( A e. H~ -> ( ( S -op T ) ` A ) e. H~ ) $=
      ( chil wf wcel chod co cfv hodclt mpanl12 ) FFBGFFCGAFHABCIJKFHDEABCLM $.
      $( [12-Nov-2000] $)

    $( Composition of Hilbert space operators. $)
    hoco $p |- ( A e. H~ -> ( ( S o. T ) ` A ) = ( S ` ( T ` A ) ) ) $=
      ( chil wcel cdm ccom cfv wceq wf fdm ax-mp eleq2i wfun ffun fvco mp3an12
      sylbir ) AFGACHZGZABCIJACJBJKZUAFAFFCLZUAFKEFFCMNOBPZCPZUBUCFFBLUEDFFBQNU
      DUFEFFCQNABCRST $.
      $( [12-Nov-2000] $)

    $( Closure of composition of Hilbert space operators. $)
    hococl $p |- ( A e. H~ -> ( ( S o. T ) ` A ) e. H~ ) $=
      ( chil wcel ccom cfv hoco ffvelrni syl eqeltrd ) AFGZABCHIACIZBIZFABCDEJN
      OFGPFGFFACEKFFOBDKLM $.
      $( [12-Nov-2000] $)

    $( Mapping of composition of Hilbert space operators. $)
    hocof $p |- ( S o. T ) : H~ --> H~ $=
      ( chil wf ccom fco mp2an ) EEAFEEBFEEABGFCDEEEABHI $.
      $( [14-Nov-2000] $)

    $( Functionality of composition of Hilbert space operators. $)
    hocofn $p |- ( S o. T ) Fn H~ $=
      ( chil ccom wf wfn hocof ffn ax-mp ) EEABFZGLEHABCDIEELJK $.
      $( [12-Nov-2000] $)

    $( Mapping of sum of Hilbert space operators. $)
    hoaddcl $p |- ( S +op T ) : H~ --> H~ $=
      ( chil wf chos co hoaddclt mp2an ) EEAFEEBFEEABGHFCDABIJ $.
      $( [14-Nov-2000] $)

    $( Mapping of difference of Hilbert space operators. $)
    hosubcl $p |- ( S -op T ) : H~ --> H~ $=
      ( vx vy chil cv cfv cmv co chod wf wcel wceq wa copab hodmvalt mp2an
      hvsubclt ffvelrni sylanc fopab ) EFGGEHZAIZUDBIZJKZABLKZGGAMGGBMUHUDGNZFH
      UGOPEFQOCDEFABRSUEGNUFGNUGGNUIUEUFTGGUDACUAGGUDBDUAUBUC $.
      $( [14-Nov-2000] $)

    $( Functionality of sum of Hilbert space operators. $)
    hoaddfn $p |- ( S +op T ) Fn H~ $=
      ( chil chos co wf wfn hoaddcl ffn ax-mp ) EEABFGZHMEIABCDJEEMKL $.
      $( [14-Nov-2000] $)

    $( Functionality of difference of Hilbert space operators. $)
    hosubfn $p |- ( S -op T ) Fn H~ $=
      ( chil chod co wf wfn hosubcl ffn ax-mp ) EEABFGZHMEIABCDJEEMKL $.
      $( [30-Jun-2006] $) $( [2-Jun-2006] $)

    $( Commutativity of sum of Hilbert space operators. $)
    hoaddcom $p |- ( S +op T ) = ( T +op S ) $=
      ( vx cv chos co cfv wceq chil wral wcel cva ax-hvcom ffvelrni sylanc wf
      hosvaltOLD mpanl12 3eqtr4d rgen hoaddcl hoeq mpbi ) EFZABGHZIZUFBAGHZIZJZ
      EKLUGUIJUKEKUFKMZUFAIZUFBIZNHZUNUMNHZUHUJUMKMUNKMUOUPJULUMUNOKKUFACPKKUFB
      DPQKKARZKKBRZULUHUOJCDUFABSTURUQULUJUPJDCUFBASTUAUBEUGUIABCDUCBADCUCUDUE
      $.
      $( [15-Nov-2000] $)
  $}

  $( Mapping of difference of Hilbert space operators. $)
  hosubclt $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ ) ->
              ( S -op T ) : H~ --> H~ ) $=
    ( chil wf chod co ch0o cif wceq opreq1 feq1d opreq2 ho0f elimf hosubcl
    dedth2h ) CCADZCCBDZCCABEFZDCCQAGHZBEFZDCCTRBGHZEFZDABGGATICCSUAATBEJKBUBIC
    CUAUCBUBTELKTUBCCAGMNCCBGMNOP $.
    $( [25-Aug-2006] $) $( [23-Aug-2006] $)

  $( Commutativity of sum of Hilbert space operators. $)
  hoaddcomt $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ ) ->
                ( S +op T ) = ( T +op S ) ) $=
    ( chil wf chos co wceq ch0o cif opreq1 opreq2 eqeq12d ho0f elimf hoaddcom
    dedth2h ) CCADZCCBDZABEFZBAEFZGQAHIZBEFZBUAEFZGUARBHIZEFZUDUAEFZGABHHAUAGSU
    BTUCAUABEJAUABEKLBUDGUBUEUCUFBUDUAEKBUDUAEJLUAUDCCAHMNCCBHMNOP $.
    $( [26-Aug-2006] $) $( [24-Aug-2006] $)

  ${
    $d x R $.  $d x S $.  $d x T $.
    hods.1 $e |- R : H~ --> H~ $.
    hods.2 $e |- S : H~ --> H~ $.
    hods.3 $e |- T : H~ --> H~ $.
    $( Relationship between Hilbert space operator difference and sum. $)
    hods $p |- ( ( R -op S ) = T <-> ( S +op T ) = R ) $=
      ( vx cv chod co cfv wceq chil wral chos wcel cmv cva wb hvsubaddt
      ffvelrni syl3anc wf hodvaltOLD mpanl12 eqeq1d hosvaltOLD 3bitr4d ralbiia
      hosubcl hoeq hoaddcl 3bitr3 ) GHZABIJZKZUNCKZLZGMNUNBCOJZKZUNAKZLZGMNUOCL
      USALURVBGMUNMPZVAUNBKZQJZUQLZVDUQRJZVALZURVBVAMPVDMPUQMPVFVHSVCVAVDUQTMMU
      NADUAMMUNBEUAMMUNCFUAUBVCUPVEUQMMAUCMMBUCZVCUPVELDEUNABUDUEUFVCUTVGVAVIMM
      CUCVCUTVGLEFUNBCUGUEUFUHUIGUOCABDEUJFUKGUSABCEFULDUKUM $.
      $( [17-Nov-2000] $)

    $( Associativity of sum of Hilbert space operators. $)
    hoaddass $p |- ( ( R +op S ) +op T ) = ( R +op ( S +op T ) ) $=
      ( vx cv chos co cfv wceq chil wral wcel cva ax-hvass ffvelrni syl3anc wf
      hoaddcl hosvaltOLD mpanl12 opreq1d eqtrd opreq2d 3eqtr4d rgen hoeq mpbi
      ) GHZABIJZCIJZKZUKABCIJZIJZKZLZGMNUMUPLURGMUKMOZUKAKZUKBKZPJZUKCKZPJZUTVA
      VCPJZPJZUNUQUTMOVAMOVCMOVDVFLUSUTVAVCQMMUKADRMMUKBERMMUKCFRSUSUNUKULKZVCP
      JZVDMMULTMMCTZUSUNVHLABDEUAZFUKULCUBUCUSVGVBVCPMMATZMMBTZUSVGVBLDEUKABUBU
      CUDUEUSUQUTUKUOKZPJZVFVKMMUOTUSUQVNLDBCEFUAZUKAUOUBUCUSVMVEUTPVLVIUSVMVEL
      EFUKBCUBUCUFUEUGUHGUMUPULCVJFUAAUODVOUAUIUJ $.
      $( [26-Nov-2000] $)

    $( Commutative/associative law for Hilbert space operator sum that swaps
       the first two terms. $)
    hoadd12 $p |- ( R +op ( S +op T ) ) = ( S +op ( R +op T ) ) $=
      ( chos co hoaddcom opreq1i hoaddass 3eqtr3 ) ABGHZCGHBAGHZCGHABCGHGHBACGH
      GHMNCGABDEIJABCDEFKBACEDFKL $.
      $( [21-Sep-2004] $) $( [27-Aug-2004] $)

    $( Commutative/associative law for Hilbert space operator sum that swaps
       the second and third terms. $)
    hoadd23 $p |- ( ( R +op S ) +op T ) = ( ( R +op T ) +op S ) $=
      ( chos co hoaddcom opreq2i hoaddass 3eqtr4 ) ABCGHZGHACBGHZGHABGHCGHACGHB
      GHMNAGBCEFIJABCDEFKACBDFEKL $.
      $( [28-Jul-2006] $) $( [27-Jul-2006] $)

    $( Distributive law for Hilbert space operator sum. $)
    hocadddir $p |- ( ( R +op S ) o. T ) = ( ( R o. T ) +op ( S o. T ) ) $=
      ( vx cv chos co ccom cfv wceq chil wral wcel cva ffvelrni wf hosvaltOLD
      mpanl12 syl hoco opreq12d eqtr4d hoaddcl hocof 3eqtr4d rgen hoeq mpbi )
      GHZABIJZCKZLZULACKZBCKZIJZLZMZGNOUNURMUTGNULNPZULCLZUMLZULUPLZULUQLZQJZUO
      USVAVCVBALZVBBLZQJZVFVAVBNPZVCVIMZNNULCFRNNASNNBSVJVKDEVBABTUAUBVAVDVGVEV
      HQULACDFUCULBCEFUCUDUEULUMCABDEUFZFUCNNUPSNNUQSVAUSVFMACDFUGZBCEFUGZULUPU
      QTUAUHUIGUNURUMCVLFUGUPUQVMVNUFUJUK $.
      $( [26-Nov-2000] $)

    $( Distributive law for Hilbert space operator difference. $)
    hocsubdir $p |- ( ( R -op S ) o. T ) = ( ( R o. T ) -op ( S o. T ) ) $=
      ( vx cv chod co ccom cfv wceq chil wral wcel cmv ffvelrni wf hodvaltOLD
      mpanl12 syl hoco opreq12d eqtr4d hosubcl hocof 3eqtr4d rgen hoeq mpbi )
      GHZABIJZCKZLZULACKZBCKZIJZLZMZGNOUNURMUTGNULNPZULCLZUMLZULUPLZULUQLZQJZUO
      USVAVCVBALZVBBLZQJZVFVAVBNPZVCVIMZNNULCFRNNASNNBSVJVKDEVBABTUAUBVAVDVGVEV
      HQULACDFUCULBCEFUCUDUEULUMCABDEUFZFUCNNUPSNNUQSVAUSVFMACDFUGZBCEFUGZULUPU
      QTUAUHUIGUNURUMCVLFUGUPUQVMVNUFUJUK $.
      $( [26-Nov-2000] $)

    $( Double composition of Hilbert space operators. $)
    ho2co $p |- ( A e. H~ -> ( ( ( R o. S ) o. T ) ` A ) =
                 ( R ` ( S ` ( T ` A ) ) ) ) $=
      ( chil wcel ccom cfv hocof hoco wceq ffvelrni syl eqtrd ) AHIZABCJZDJKADK
      ZSKZTCKBKZASDBCEFLGMRTHIUAUBNHHADGOTBCEFMPQ $.
      $( [1-Dec-2000] $)
  $}

  $( Associativity of sum of Hilbert space operators. $)
  hoaddasst $p |- ( ( R : H~ --> H~ /\ S : H~ --> H~ /\ T : H~ --> H~ ) ->
                ( ( R +op S ) +op T ) = ( R +op ( S +op T ) ) ) $=
    ( chil wf chos co wceq ch0o cif opreq1 opreq1d eqeq12d opreq2 opreq2d ho0f
    elimf hoaddass dedth3h ) DDAEZDDBEZDDCEZABFGZCFGZABCFGZFGZHTAIJZBFGZCFGZUGU
    EFGZHABCIIIUGUABIJZFGZCFGZUGUKCFGZFGZHULUBCIJZFGZUGUKUPFGZFGZHAUGHZUDUIUFUJ
    UTUCUHCFAUGBFKLAUGUEFKMBUKHZUIUMUJUOVAUHULCFBUKUGFNLVAUEUNUGFBUKCFKOMCUPHZU
    MUQUOUSCUPULFNVBUNURUGFCUPUKFNOMUGUKUPDDAIPQDDBIPQDDCIPQRS $.
    $( [25-Aug-2006] $) $( [24-Aug-2006] $)

  $( Commutative/associative law for Hilbert space operator sum that swaps
     the second and third terms. $)
  hoadd23t $p |- ( ( R : H~ --> H~ /\ S : H~ --> H~ /\ T : H~ --> H~ ) ->
                ( ( R +op S ) +op T ) = ( ( R +op T ) +op S ) ) $=
    ( chil wf w3a chos co wceq hoaddcomt 3adant1 opreq2d hoaddasst 3com23
    3eqtr4d ) DDAEZDDBEZDDCEZFZABCGHZGHACBGHZGHZABGHCGHACGHBGHZSTUAAGQRTUAIPBCJ
    KLABCMPRQUCUBIACBMNO $.
    $( [26-Aug-2006] $) $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a sum of Hilbert space operators. $)
  hoadd4t $p |- ( ( ( R : H~ --> H~ /\ S : H~ --> H~ ) /\
      ( T : H~ --> H~ /\ U : H~ --> H~ ) ) -> ( ( R +op S ) +op ( T +op U ) ) =
                ( ( R +op T ) +op ( S +op U ) ) ) $=
    ( chil wf wa chos co wceq w3a hoadd23t opreq1d 3expa adantrr hoaddasst
    3expb hoaddclt sylan an4s 3eqtr3d ) EEAFZEEBFZGZEECFZEEDFZGZGABHIZCHIZDHIZA
    CHIZBHIZDHIZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOEEUHF
    ZUGUJUNJZUDUQUEUFURUHCDPQABRSUBUEUCUFUMUOJZEEUKFZUCUFGUSUBUEGUTUCUFUSUKBDPQ
    ACRSTUA $.
    $( [27-Aug-2006] $) $( [24-Aug-2006] $)

  $( Distributive law for Hilbert space operator difference. $)
  hocsubdirt $p |- ( ( R : H~ --> H~ /\ S : H~ --> H~ /\ T : H~ --> H~ ) ->
                ( ( R -op S ) o. T ) = ( ( R o. T ) -op ( S o. T ) ) ) $=
    ( chil wf chod co ccom wceq ch0o cif opreq1 coeq1d coeq1 opreq1d eqeq12d
    opreq2 opreq2d coeq2 opreq12d ho0f elimf hocsubdir dedth3h ) DDAEZDDBEZDDCE
    ZABFGZCHZACHZBCHZFGZIUEAJKZBFGZCHZUMCHZUKFGZIABCJJJUMUFBJKZFGZCHZUPURCHZFGZ
    IUSUGCJKZHZUMVCHZURVCHZFGZIAUMIZUIUOULUQVHUHUNCAUMBFLMVHUJUPUKFAUMCNOPBURIZ
    UOUTUQVBVIUNUSCBURUMFQMVIUKVAUPFBURCNRPCVCIZUTVDVBVGCVCUSSVJUPVEVAVFFCVCUMS
    CVCURSTPUMURVCDDAJUAUBDDBJUAUBDDCJUAUBUCUD $.
    $( [27-Aug-2006] $) $( [23-Aug-2006] $)

  ${
    $d x T $.
    hoaddid1.1 $e |- T : H~ --> H~ $.
    $( Sum of a Hilbert space operator with the zero operator. $)
    hoaddid1 $p |- ( T +op 0hop ) = T $=
      ( vx cv ch0o chos co cfv wceq chil wral wcel cva c0v wf ho0f hosvalt
      mp3an12 ho0valt opreq2d ffvelrni ax-hvaddid syl 3eqtrd rgen hoaddcl hoeq
      mpbi ) CDZAEFGZHZUIAHZIZCJKUJAIUMCJUIJLZUKULUIEHZMGZULNMGZULJJAOJJEOUNUKU
      PIBPUIAEQRUNUONULMUISTUNULJLUQULIJJUIABUAULUBUCUDUECUJAAEBPUFBUGUH $.
      $( [13-Aug-2006] $) $( [15-Nov-2000] $)

    $( Difference of a Hilbert space operator from itself. $)
    hodid $p |- ( T -op T ) = 0hop $=
      ( chod co ch0o wceq chos hoaddid1 ho0f hods mpbir ) AACDEFAEGDAFABHAAEBBI
      JK $.
      $( [30-Jul-2007] $) $( [10-Mar-2006] $)

    $( Composition of the zero operator and a Hilbert space operator. $)
    ho0co $p |- ( 0hop o. T ) = 0hop $=
      ( vx cv ch0o ccom cfv wceq chil wral wcel c0v ffvelrni ho0valt syl ho0f
      hoco 3eqtr4d rgen hocof hoeq mpbi ) CDZEAFZGZUCEGZHZCIJUDEHUGCIUCIKZUCAGZ
      EGZLUEUFUHUIIKUJLHIIUCABMUINOUCEAPBQUCNRSCUDEEAPBTPUAUB $.
      $( [9-Aug-2006] $) $( [9-Aug-2006] $)

    $( Composition of Hilbert space operator with unit identity. $)
    hoid1 $p |- ( T o. Iop ) = T $=
      ( vx chio ccom chil cpj cfv df-iop coeq2i cv wceq wral wcel helch pjf
      hoco pjch1t fveq2d eqtrd rgen hocof hoeq mpbi eqtr ) ADEAFGHZEZADUFAIJCKZ
      UGHZUHAHZLZCFMUGALUKCFUHFNZUIUHUFHZAHUJUHAUFBFOPZQULUMUHAUHRSTUACUGAAUFBU
      NUBBUCUDUE $.
      $( [18-Sep-2007] $) $( [15-Nov-2000] $)

    $( Composition of Hilbert space operator with unit identity. $)
    hoid1r $p |- ( Iop o. T ) = T $=
      ( vx chio ccom chil cpj cfv df-iop coeq1i cv wceq wral wcel helch pjf
      hoco ffvelrni pjch1t syl eqtrd rgen hocof hoeq mpbi eqtr ) DAEFGHZAEZADUG
      AIJCKZUHHZUIAHZLZCFMUHALULCFUIFNZUJUKUGHZUKUIUGAFOPZBQUMUKFNUNUKLFFUIABRU
      KSTUAUBCUHAUGAUOBUCBUDUEUF $.
      $( [17-Nov-2007] $) $( [15-Nov-2000] $)
  $}

  $( Sum of a Hilbert space operator with the zero operator. $)
  hoaddid1t $p |- ( T : H~ --> H~ -> ( T +op 0hop ) = T ) $=
    ( chil wf ch0o chos co wceq cif opreq1 id eqeq12d ho0f elimf hoaddid1
    dedth ) BBACZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRBBADLMNO $.
    $( [31-Jul-2007] $) $( [25-Jul-2006] $)

  $( Difference of a Hilbert space operator from itself. $)
  hodidt $p |- ( T : H~ --> H~ -> ( T -op T ) = 0hop ) $=
    ( chil wf chod co ch0o wceq cif id opreq12d eqeq1d ho0f elimf hodid dedth
    ) BBACZAADEZFGPAFHZRDEZFGAFARGZQSFTARARDTIZUAJKRBBAFLMNO $.
    $( [31-Jul-2007] $) $( [23-Jul-2006] $)

  $( A Hilbert space operator is not empty. $)
  hon0 $p |- ( T : H~ --> H~ -> -. T = (/) ) $=
    ( chil wf c0 wceq c0v wcel wn ax-hv0cl n0i ax-mp wfn wi ffn fndmu ex syl
    fn0 syl5ibr mtoi ) BBACZADEZBDEZFBGUCHIBFJKUAADLZUCUBUAABLZUDUCMBBANUEUDUCB
    DAOPQARST $.
    $( [23-May-2006] $) $( [24-Mar-2006] $)

  ${
    $d x S $.  $d x T $.
    hodseq.2 $e |- S : H~ --> H~ $.
    hodseq.3 $e |- T : H~ --> H~ $.
    $( Subtraction and addition of equal Hilbert space operators. $)
    hodseq $p |- ( S +op ( T -op S ) ) = T $=
      ( chod co wceq chos eqid hosubcl hods mpbi ) BAEFZMGAMHFBGMIBAMDCBADCJKL
      $.
      $( [22-Oct-2004] $) $( [27-Aug-2004] $)

    $( Subtraction of Hilbert space operators expressed in terms of difference
       from zero. $)
    ho0sub $p |- ( S -op T ) = ( S +op ( 0hop -op T ) ) $=
      ( chod co ch0o chos wceq ho0f hosubcl hoadd12 hodseq opreq2i hoaddid1
      3eqtr hoaddcl hods mpbir ) ABEFAGBEFZHFZIBUAHFZAIUBABTHFZHFAGHFABATDCGBJD
      KZLUCGAHBGDJMNACOPABUACDATCUDQRS $.
      $( [13-Aug-2006] $) $( [10-Mar-2006] $)

    $( Relationship between Hilbert operator addition and subtraction. $)
    honegsub $p |- ( S +op ( -u 1 .op T ) ) = ( S -op T ) $=
      ( vx cv c1 cneg chot co chos cfv chod wceq chil wral wcel cmv cva csm
      hvsubvalt ffvelrni sylanc cc wf ax1cn negcl homvalt mp3an12 opreq2d
      eqtr4d hodvalt homulclt mp2an hosvalt 3eqtr4rd rgen hoaddcl hosubcl hoeq
      mpbi ) EFZAGHZBIJZKJZLZVBABMJZLZNZEOPVEVGNVIEOVBOQZVBALZVBBLZRJZVKVBVDLZS
      JZVHVFVJVMVKVCVLTJZSJZVOVKOQVLOQVMVQNVJVKVLUAOOVBACUBOOVBBDUBUCVJVNVPVKSV
      CUDQZOOBUEZVJVNVPNGUFUGZDVCVBBUHUIUJUKOOAUEZVSVJVHVMNCDVBABULUIWAOOVDUEZV
      JVFVONCVRVSWBVTDVCBUMUNZVBAVDUOUIUPUQEVEVGAVDCWCURABCDUSUTVA $.
      $( [27-Aug-2006] $) $( [24-Aug-2006] $)

$(
    @( Distributive law for Hilbert space operators. @)
    hosdi @p |- ( A e. CC ->
                 ( A .op ( S +op T ) ) = ( ( A .op S ) +op ( A .op T ) ) ) @=
?@.

    @( Distributive law for Hilbert space operators. @)
    hoddi @p |- ( A e. CC ->
                 ( A .op ( S -op T ) ) = ( ( A .op S ) -op ( A .op T ) ) ) @=
?@.
$)
  $}

  $( Subtraction of Hilbert space operators expressed in terms of difference
     from zero. $)
  ho0subt $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ ) ->
                ( S -op T ) = ( S +op ( 0hop -op T ) ) ) $=
    ( chil wf chod co ch0o chos wceq cif opreq1 eqeq12d opreq2 opreq2d ho0f
    elimf ho0sub dedth2h ) CCADZCCBDZABEFZAGBEFZHFZISAGJZBEFZUDUBHFZIUDTBGJZEFZ
    UDGUGEFZHFZIABGGAUDIUAUEUCUFAUDBEKAUDUBHKLBUGIZUEUHUFUJBUGUDEMUKUBUIUDHBUGG
    EMNLUDUGCCAGOPCCBGOPQR $.
    $( [16-Aug-2006] $) $( [25-Jul-2006] $)

  $( The zero operator subtracted from a Hilbert space operator. $)
  hosubid1t $p |- ( T : H~ --> H~ -> ( T -op 0hop ) = T ) $=
    ( chil wf ch0o chod co chos wceq ho0f ho0subt mpan2 hodid opreq2i a1i
    hoaddid1t 3eqtrd ) BBACZADEFZADDEFZGFZADGFZAQBBDCRTHIADJKTUAHQSDAGDILMNAOP
    $.
    $( [16-Aug-2006] $) $( [25-Jul-2006] $)

  $( Relationship between Hilbert space operator addition and subtraction. $)
  honegsubt $p |- ( ( T : H~ --> H~ /\ U : H~ --> H~ ) ->
                ( T +op ( -u 1 .op U ) ) = ( T -op U ) ) $=
    ( chil wf c1 cneg chot co chos chod wceq ch0o cif opreq1 eqeq12d opreq2
    opreq2d ho0f elimf honegsub dedth2h ) CCADZCCBDZAEFZBGHZIHZABJHZKUBALMZUEIH
    ZUHBJHZKUHUDUCBLMZGHZIHZUHUKJHZKABLLAUHKUFUIUGUJAUHUEINAUHBJNOBUKKZUIUMUJUN
    UOUEULUHIBUKUDGPQBUKUHJPOUHUKCCALRSCCBLRSTUA $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  ${
    $d x A $.  $d x B $.  $d x T $.  $d x U $.
    $( An operator equals its scalar product with one. $)
    homulid2t $p |- ( T : H~ --> H~ -> ( 1 .op T ) = T ) $=
      ( vx chil wf cv c1 chot co cfv wceq wral wcel wa csm cc ax1cn homvalt
      mp3an1 ffvelrn ax-hvmulid syl eqtrd r19.21aiva wb homulclt mpan hoeqt
      mpancom mpbid ) CCADZBEZFAGHZIZUKAIZJZBCKZULAJZUJUOBCUJUKCLZMZUMFUNNHZUNF
      OLZUJURUMUTJPFUKAQRUSUNCLUTUNJCCUKASUNTUAUBUCCCULDZUJUPUQUDVAUJVBPFAUEUFB
      ULAUGUHUI $.
      $( [16-Aug-2006] $) $( [12-Aug-2006] $)

    $( Associative law for scalar product and composition of operators. $)
    homco1t $p |- ( ( A e. CC /\ T : H~ --> H~ /\ U : H~ --> H~ ) ->
                  ( ( A .op T ) o. U ) = ( A .op ( T o. U ) ) ) $=
      ( vx cc wcel chil wf w3a cv chot co ccom cfv wceq wral wa csm homvalt
      ffvelrn syl3an3 3expa exp43 3imp1 wi wfun fvco3 homulclt ffun syl
      syl3an1 3expia 3impa imp opreq2d 3adantl1 3eqtr4d fco syl3an2 3impb
      eqtr4d r19.21aiva wb hoeqt sylan sylan2 sylanc mpbid ) AEFZGGBHZGGCHZIZDJ
      ZABKLZCMZNZVMABCMZKLZNZOZDGPZVOVROZVLVTDGVLVMGFZQZVPAVMVQNZRLZVSWDVMCNZVN
      NZAWGBNZRLZVPWFVIVJVKWCWHWJOZVIVJVKWCWKVIVJVKWCQZWKVIVJWGGFWKWLAWGBSGGVMC
      TUAUBUCUDVLWCVPWHOZVIVJVKWCWMUEVIVJQZVKWCWMVNUFZVKWCWMWNGGVMVNCUGWNGGVNHZ
      WOABUHZGGVNUIUJUKULUMUNVJVKWCWFWJOVIVJVKQZWCQWEWIARVJVKWCWEWIOZBUFVKWCWSV
      JGGVMBCUGGGBUIUKUBUOUPUQVLWCVSWFOZVIVJVKWCWTUEVIWRWCWTVIGGVQHZWCWTWRAVMVQ
      SGGGBCURZUSULUTUNVAVBGGVOHZGGVRHZWAWBVCVLDVOVRVDVIVJVKXCWPVKXCWNGGGVNCURW
      QVEUMVIVJVKXDVIXAXDWRAVQUHXBVFUTVGVH $.
      $( [20-Aug-2006] $) $( [13-Aug-2006] $)

    $( Scalar product associative law for Hilbert space operators. $)
    homulasst $p |- ( ( A e. CC /\ B e. CC /\ T : H~ --> H~ ) ->
                  ( ( A x. B ) .op T ) = ( A .op ( B .op T ) ) ) $=
      ( vx cc wcel chil wf w3a cv cmul co chot cfv wceq wral wa csm
      ax-hvmulass ffvelrn syl3an3 3expa exp43 3imp1 wi homvalt axmulcl syl3an1
      3expia 3impa imp opreq2d 3adantl1 3eqtr4d homulclt syl3an2 3impb eqtr4d
      r19.21aiva wb hoeqt sylan sylan2 sylanc mpbid ) AEFZBEFZGGCHZIZDJZABKLZCM
      LZNZVJABCMLZMLZNZOZDGPZVLVOOZVIVQDGVIVJGFZQZVMAVJVNNZRLZVPWAVKVJCNZRLZABW
      DRLZRLZVMWCVFVGVHVTWEWGOZVFVGVHVTWHVFVGVHVTQZWHVFVGWDGFWHWIABWDSGGVJCTUAU
      BUCUDVIVTVMWEOZVFVGVHVTWJUEVFVGQZVHVTWJVKEFZVHVTWJWKVKVJCUFABUGZUHUIUJUKV
      GVHVTWCWGOZVFVGVHVTWNVGVHVTIWBWFARBVJCUFULUBUMUNVIVTVPWCOZVFVGVHVTWOUEVFV
      GVHQZVTWOVFGGVNHZVTWOWPAVJVNUFBCUOZUPUIUQUKURUSGGVLHZGGVOHZVRVSUTVIDVLVOV
      AVFVGVHWSWLVHWSWKVKCUOWMVBUJVFVGVHWTVFWQWTWPAVNUOWRVCUQVDVE $.
      $( [18-Aug-2006] $) $( [12-Aug-2006] $)

    $( Scalar product distributive law for Hilbert space operators. $)
    hoadddit $p |- ( ( A e. CC /\ T : H~ --> H~ /\ U : H~ --> H~ ) ->
                 ( A .op ( T +op U ) ) = ( ( A .op T ) +op ( A .op U ) ) ) $=
      ( vx cc wcel chil wf w3a cv chos co chot cfv wceq wral wa cva csm
      homvalt 3expa hoaddclt anim2i 3impb sylan hosvalt opreq2d 3adantl1
      ax-hvdistr1 3simp1 adantr ffvelrn 3ad2antl2 3ad2antl3 syl3anc 3adantl3
      3adantl2 opreq12d eqtr4d 3eqtrd homulclt anim12i 3impdi r19.21aiva wb
      hoeqt sylan2 syl2an sylanc mpbid ) AEFZGGBHZGGCHZIZDJZABCKLZMLZNZVOABMLZA
      CMLZKLZNZOZDGPZVQWAOZVNWCDGVNVOGFZQZVRVOVSNZVOVTNZRLZWBWGVRAVOVPNZSLZAVOB
      NZVOCNZRLZSLZWJVKGGVPHZQZWFVRWLOZVNVKWQWFWSAVOVPTUAVKVLVMWRVLVMQZWQVKBCUB
      ZUCUDUEVLVMWFWLWPOZVKVLVMWFXBVLVMWFIWKWOASVOBCUFUGUAUHWGWPAWMSLZAWNSLZRLZ
      WJVKWMGFZWNGFZWPXEOWGAWMWNUIVNVKWFVKVLVMUJUKVLVKWFXFVMGGVOBULUMVMVKWFXGVL
      GGVOCULUNUOWGWHXCWIXDRVKVLWFWHXCOZVMVKVLWFXHAVOBTUAUPVKVMWFWIXDOZVLVKVMWF
      XIAVOCTUAUQURUSUTGGVSHZGGVTHZQZWFWBWJOZVNXJXKWFXMVOVSVTUFUAVKVLVMXLVKVLQZ
      XJVKVMQZXKABVAZACVAZVBVCUEUSVDGGVQHZGGWAHZWDWEVEVNDVQWAVFVKVLVMXRVKWQXRWT
      AVPVAXAVGUDVKVLVMXSXJXKXSXNXOVSVTUBXPXQVHVCVIVJ $.
      $( [19-Aug-2006] $) $( [12-Aug-2006] $)

    $( Scalar product reverse distributive law for Hilbert space operators. $)
    hoadddirt $p |- ( ( A e. CC /\ B e. CC /\ T : H~ --> H~ ) ->
                 ( ( A + B ) .op T ) = ( ( A .op T ) +op ( B .op T ) ) ) $=
      ( vx cc wcel chil wf w3a cv caddc co chot cfv chos wceq wral wa cva csm
      ax-hvdistr2 ffvelrn syl3an3 3exp exp4a 3imp1 homvalt 3expa axaddcl
      anim1i 3impa sylan 3adantl2 3adantl1 opreq12d 3eqtr4d hosvalt homulclt
      anim12i 3impdir eqtr4d r19.21aiva wb hoeqt hoaddclt syl2an sylanc mpbid
      ) AEFZBEFZGGCHZIZDJZABKLZCMLZNZVMACMLZBCMLZOLZNZPZDGQZVOVSPZVLWADGVLVMGFZ
      RZVPVMVQNZVMVRNZSLZVTWEVNVMCNZTLZAWITLZBWITLZSLZVPWHVIVJVKWDWJWMPZVIVJVKW
      DWNVIVJVKWDRZWNVIVJWIGFWNWOABWIUAGGVMCUBUCUDUEUFVNEFZVKRZWDVPWJPZVLWPVKWD
      WRVNVMCUGUHVIVJVKWQVIVJRZWPVKABUIZUJUKULWEWFWKWGWLSVIVKWDWFWKPZVJVIVKWDXA
      AVMCUGUHUMVJVKWDWGWLPZVIVJVKWDXBBVMCUGUHUNUOUPGGVQHZGGVRHZRZWDVTWHPZVLXCX
      DWDXFVMVQVRUQUHVIVKVJXEVIVKRZXCVJVKRZXDACURZBCURZUSUTULVAVBGGVOHZGGVSHZWB
      WCVCVLDVOVSVDVIVJVKXKWPVKXKWSVNCURWTULUKVIVKVJXLXCXDXLXGXHVQVRVEXIXJVFUTV
      GVH $.
      $( [2-Sep-2006] $) $( [25-Aug-2006] $)
  $}

  $( Swap first and second factors in a nested operator scalar product. $)
  homul12t $p |- ( ( A e. CC /\ B e. CC /\ T : H~ --> H~ ) ->
                ( A .op ( B .op T ) ) = ( B .op ( A .op T ) ) ) $=
    ( cc wcel chil wf w3a cmul co chot wceq wa axmulcom opreq1d 3adant3
    homulasst 3com12 3eqtr3d ) ADEZBDEZFFCGZHABIJZCKJZBAIJZCKJZABCKJKJBACKJKJZT
    UAUDUFLUBTUAMUCUECKABNOPABCQUATUBUFUGLBACQRS $.
    $( [19-Aug-2006] $) $( [12-Aug-2006] $)

  $( Double negative of a Hilbert space operator. $)
  honegnegt $p |- ( T : H~ --> H~ -> ( -u 1 .op ( -u 1 .op T ) ) = T ) $=
    ( chil wf c1 cneg cmul co chot wceq ax1cn mul2neg mulid1 eqtr opreq1i a1i
    cc wcel negcl homulasst mp3an12 homulid2t 3eqtr3d ) BBACZDEZUDFGZAHGZDAHGZU
    DUDAHGHGZAUFUGIUCUEDAHUEDDFGDDDJJKDJLMNOUDPQZUIUCUFUHIDJRZUJUDUDASTAUAUB $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  $( Relationship between operator subtraction and negative. $)
  hosubnegt $p |- ( ( T : H~ --> H~ /\ U : H~ --> H~ ) ->
                  ( T -op ( -u 1 .op U ) ) = ( T +op U ) ) $=
    ( chil wf wa c1 cneg chot co chos chod wceq honegsubt cc wcel ax1cn negcl
    homulclt mpan sylan2 honegnegt opreq2d adantl eqtr3d ) CCADZCCBDZEAFGZUGBHI
    ZHIZJIZAUHKIZABJIZUECCUHDZUJUKLUFAUHMUGNOUFUMFPQUGBRSTUFUJULLUEUFUIBAJBUAUB
    UCUD $.
    $( [2-Sep-2006] $) $( [25-Aug-2006] $)

  $( Scalar product distributive law for operator difference. $)
  hosubdit $p |- ( ( A e. CC /\ T : H~ --> H~ /\ U : H~ --> H~ ) ->
               ( A .op ( T -op U ) ) = ( ( A .op T ) -op ( A .op U ) ) ) $=
    ( cc wcel chil wf w3a c1 cneg chot co chos chod wceq hoadddit ax1cn negcl
    homulclt mpan syl3an3 homul12t mp3an2 3adant2 opreq2d eqtrd wa honegsubt
    3adant1 syl2an 3impdi 3eqtr3d ) ADEZFFBGZFFCGZHZABIJZCKLZMLZKLZABKLZUQACKLZ
    KLZMLZABCNLZKLZVAVBNLZUPUTVAAURKLZMLZVDUMUNFFURGZUTVIOUOABURPUQDEZUOVJIQRZU
    QCSTUAUPVHVCVAMUMUOVHVCOZUNUMVKUOVMVLAUQCUBUCUDUEUFUNUOUTVFOUMUNUOUGUSVEAKB
    CUHUEUIUMUNUOVDVGOZFFVAGFFVBGVNUMUNUGUMUOUGVAVBUHABSACSUJUKUL $.
    $( [19-Aug-2006] $) $( [12-Aug-2006] $)

  $( Distribution of negative over addition. $)
  honegdit $p |- ( ( T : H~ --> H~ /\ U : H~ --> H~ ) ->
          ( -u 1 .op ( T +op U ) ) = ( ( -u 1 .op T ) +op ( -u 1 .op U ) ) ) $=
    ( c1 cneg cc wcel chil wf chos co chot wceq ax1cn negcl hoadddit mp3an1 )
    CDZEFGGAHGGBHQABIJKJQAKJQBKJIJLCMNQABOP $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  $( Distribution of negative over subtraction. $)
  honegsubdit $p |- ( ( T : H~ --> H~ /\ U : H~ --> H~ ) ->
                    ( -u 1 .op ( T -op U ) ) = ( ( -u 1 .op T ) +op U ) ) $=
    ( chil wf wa c1 cneg chot co chos chod wceq honegdit cc wcel ax1cn negcl
    homulclt mpan sylan2 honegsubt opreq2d honegnegt adantl 3eqtr3d ) CCADZCCBD
    ZEZFGZAUIBHIZJIZHIZUIAHIZUIUJHIZJIZUIABKIZHIUMBJIUFCCUJDZULUOLUGAUJMUINOUGU
    QFPQUIBRSTUHUKUPUIHABUAUBUHUNBUMJUGUNBLUFBUCUDUBUE $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  $( Distribution of negative over subtraction. $)
  honegsubdi2t $p |- ( ( T : H~ --> H~ /\ U : H~ --> H~ ) ->
                     ( -u 1 .op ( T -op U ) ) = ( U -op T ) ) $=
    ( chil wf wa c1 cneg chod co chot chos honegsubdit wceq hoaddcomt cc wcel
    ax1cn negcl homulclt mpan sylan honegsubt ancoms 3eqtrd ) CCADZCCBDZEFGZABH
    IJIUGAJIZBKIZBUHKIZBAHIZABLCCUHDZUFUIUJMUEUHBNUGOPUEULFQRUGASTUAUFUEUJUKMBA
    UBUCUD $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  $( Law for double subtraction of Hilbert space operators. $)
  hosubsub2t $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ /\ U : H~ --> H~ ) ->
                  ( S -op ( T -op U ) ) = ( S +op ( U -op T ) ) ) $=
    ( chil wf w3a c1 cneg chod co chot chos wceq wa honegsubt hosubclt sylan2
    3impb honegsubdi2t opreq2d 3adant1 eqtr3d ) DDAEZDDBEZDDCEZFAGHBCIJZKJZLJZA
    UFIJZACBIJZLJZUCUDUEUHUIMZUCDDUFEULUDUENZAUFOBCPQRUDUEUHUKMUCUMUGUJALBCSTUA
    UB $.
    $( [3-Sep-2006] $) $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a mixed addition and subtraction of Hilbert
     space operators. $)
  hosub4t $p |- ( ( ( R : H~ --> H~ /\ S : H~ --> H~ ) /\
      ( T : H~ --> H~ /\ U : H~ --> H~ ) ) -> ( ( R +op S ) -op ( T +op U ) ) =
                ( ( R -op T ) +op ( S -op U ) ) ) $=
    ( chil wf wa chos co c1 cneg chot chod wceq honegdit adantl opreq2d
    hoadd4t cc wcel ax1cn negcl homulclt mpan anim12i sylan2 eqtrd honegsubt
    hoaddclt syl2an ad2ant2r ad2ant2l opreq12d 3eqtr3d ) EEAFZEEBFZGZEECFZEEDFZ
    GZGZABHIZJKZCDHIZLIZHIZAVCCLIZHIZBVCDLIZHIZHIZVBVDMIZACMIZBDMIZHIVAVFVBVGVI
    HIZHIZVKVAVEVOVBHUTVEVONUQCDOPQUQEEVGFZEEVIFZGVPVKNUTABVGVIRURVQUSVRVCSTZUR
    VQJUAUBZVCCUCUDVSUSVRVTVCDUCUDUEUFUGEEVBFEEVDFVFVLNUQUTVBVDUHABUICDUIUJVAVH
    VMVJVNHUOURVHVMNUPUSACUHUKUPUSVJVNNUOURBDUHULUMUN $.
    $( [3-Sep-2006] $) $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a mixed addition and subtraction of Hilbert
     space operators. $)
  hosubadd4t $p |- ( ( ( R : H~ --> H~ /\ S : H~ --> H~ ) /\
      ( T : H~ --> H~ /\ U : H~ --> H~ ) ) -> ( ( R -op S ) -op ( T -op U ) ) =
                ( ( R +op U ) -op ( S +op T ) ) ) $=
    ( chil wf wa chod co chos wceq hosubsub2t 3expb hosubclt sylan hosub4t
    an42s eqtr4d ) EEAFZEEBFZGZEECFZEEDFZGZGABHIZCDHIHIZUEDCHIJIZADJIBCJIHIZEEU
    EFZUDUFUGKZUAUIUBUCUJUECDLMABNOSUCTUBUHUGKADBCPQR $.
    $( [6-Sep-2006] $) $( [24-Aug-2006] $)

  $( Associative-type law for addition and subtraction of Hilbert space
     operators. $)
  hoaddsubasst $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ /\ U : H~ --> H~ ) ->
                  ( ( S +op T ) -op U ) = ( S +op ( T -op U ) ) ) $=
    ( chil wf w3a chos co ch0o chod wceq hoaddasst ho0f hosubclt mpan syl3an3
    wa ho0subt hoaddclt sylan 3impa 3adant1 opreq2d 3eqtr4d ) DDAEZDDBEZDDCEZFZ
    ABGHZICJHZGHZABUJGHZGHZUICJHZABCJHZGHUEUFDDUJEZUKUMKUGABUJLDDIEUGUPMICNOPUE
    UFUGUNUKKZDDUIEUGUQUEUFQUICRABSTUAUHUOULAGUFUGUOULKUEBCRUBUCUD $.
    $( [25-Aug-2006] $) $( [25-Aug-2006] $)

  $( Law for operator addition and subtraction of Hilbert space operators. $)
  hoaddsubt $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ /\ U : H~ --> H~ ) ->
                  ( ( S +op T ) -op U ) = ( ( S -op U ) +op T ) ) $=
    ( chil wf w3a chos co chod wceq wa hoaddcomt opreq1d 3adant3 hoaddsubasst
    3com12 wi ex hosubclt syl5 exp3a com12 3imp 3eqtrd ) DDAEZDDBEZDDCEZFABGHZC
    IHZBAGHZCIHZBACIHZGHZULBGHZUEUFUIUKJUGUEUFKUHUJCIABLMNUFUEUGUKUMJBACOPUEUFU
    GUMUNJZUFUEUGUOQUFUEUGUOUFDDULEZUOUEUGKUFUPUOBULLRACSTUAUBUCUD $.
    $( [9-Sep-2006] $) $( [25-Aug-2006] $)

  $( Law for double subtraction of Hilbert space operators. $)
  hosubsubt $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ /\ U : H~ --> H~ ) ->
                  ( S -op ( T -op U ) ) = ( ( S -op T ) +op U ) ) $=
    ( chil wf w3a chod co chos hosubsub2t wceq hoaddsubasst hoaddsubt eqtr3d
    3com23 eqtrd ) DDAEZDDBEZDDCEZFABCGHGHACBGHIHZABGHCIHZABCJQSRTUAKQSRFACIHBG
    HTUAACBLACBMNOP $.
    $( [9-Sep-2006] $) $( [25-Aug-2006] $)

  $( Law for double subtraction of Hilbert space operators. $)
  hosubsub4t $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ /\ U : H~ --> H~ ) ->
                  ( ( S -op T ) -op U ) = ( S -op ( T +op U ) ) ) $=
    ( chil wf w3a c1 cneg chot co chod chos wceq hosubsubt cc wcel ax1cn negcl
    homulclt mpan syl3an3 hosubnegt 3adant1 opreq2d wa honegsubt hosubclt
    sylan 3impa 3eqtr3rd ) DDAEZDDBEZDDCEZFZABGHZCIJZKJZKJZABKJZUPLJZABCLJZKJUS
    CKJZUKULDDUPEZURUTMUMABUPNUOOPUMVCGQRUOCSTUAUNUQVAAKULUMUQVAMUKBCUBUCUDUKUL
    UMUTVBMZDDUSEUMVDUKULUEUSCUFABUGUHUIUJ $.
    $( [10-Sep-2006] $) $( [25-Aug-2006] $)

  $( Two times a Hilbert space operator. $)
  ho2timest $p |- ( T : H~ --> H~ -> ( 2 .op T ) = ( T +op T ) ) $=
    ( chil wf c2 chot co c1 chos caddc cc wcel wceq ax1cn hoadddirt mp3an12
    df-2 opreq1i syl5eq hoadddit mp3an1 anidms hoaddclt homulid2t syl 3eqtr2d
    ) BBACZDAEFZGAEFZUHHFZGAAHFZEFZUJUFGGIFZAEFZUIUGGJKZUNUFUMUILMMGGANODULAEPQ
    RUFUKUILZUNUFUFUOMGAASTUAUFBBUJCZUKUJLUFUPAAUBUAUJUCUDUE $.
    $( [10-Sep-2006] $) $( [26-Aug-2006] $)

  ${
    hoaddsubass.1 $e |- R : H~ --> H~ $.
    hoaddsubass.2 $e |- S : H~ --> H~ $.
    hoaddsubass.3 $e |- T : H~ --> H~ $.
    $( Associativity of sum and difference of Hilbert space operators. $)
    hoaddsubass $p |- ( ( R +op S ) -op T ) = ( R +op ( S -op T ) ) $=
      ( chil wf chos co chod wceq hoaddsubasst mp3an ) GGAHGGBHGGCHABIJCKJABCKJ
      IJLDEFABCMN $.
      $( [18-Nov-2004] $) $( [27-Aug-2004] $)

    $( Law for sum and difference of Hilbert space operators. $)
    hoaddsub $p |- ( ( R +op S ) -op T ) = ( ( R -op T ) +op S ) $=
      ( chos co chod hoaddcom opreq1i hoaddsubass hosubcl 3eqtr ) ABGHZCIHBAGHZ
      CIHBACIHZGHQBGHOPCIABDEJKBACEDFLBQEACDFMJN $.
      $( [18-Nov-2004] $) $( [27-Aug-2004] $)
  $}

  ${
    hosd1.2 $e |- T : H~ --> H~ $.
    hosd1.3 $e |- U : H~ --> H~ $.
    $( Hilbert space operator sum expressed in terms of difference. $)
    hosd1 $p |- ( T +op U ) = ( T -op ( 0hop -op U ) ) $=
      ( ch0o chod co chos wceq ho0f hosubcl hoaddcl hoaddcom hoaddsubass
      hoaddid1 opreq1i hoaddsub hodseq eqtr 3eqtr 3eqtr2 hods mpbir eqcomi ) AE
      BFGZFGZABHGZUFUGIUEUGHGZAIUHUGUEHGUGEHGZBFGZAUEUGEBJDKZABCDLZMUGEBULJDNUJ
      UGBFGABFGZBHGZAUIUGBFUGULOPABBCDDQUNBUMHGAUMBABCDKDMBADCRSTUAAUEUGCUKULUB
      UCUD $.
      $( [3-Aug-2007] $) $( [27-Aug-2004] $)

    $( Hilbert space operator sum expressed in terms of difference. $)
    hosd2 $p |- ( T +op U ) = ( T -op ( ( U -op U ) -op U ) ) $=
      ( chos co ch0o chod hosd1 hodid opreq1i opreq2i eqtr4 ) ABEFAGBHFZHFABBHF
      ZBHFZHFABCDIPNAHOGBHBDJKLM $.
      $( [18-Nov-2004] $) $( [27-Aug-2004] $)

    $( Hilbert space operator cancellation law. $)
    hopncan $p |- ( ( T +op U ) -op U ) = T $=
      ( chos co chod ch0o hoaddsubass hodid opreq2i hoaddid1 3eqtr ) ABEFBGFABB
      GFZEFAHEFAABBCDDINHAEBDJKACLM $.
      $( [25-Mar-2006] $) $( [10-Mar-2006] $)

    $( Hilbert space operator cancellation law. $)
    honpcan $p |- ( ( T -op U ) +op U ) = T $=
      ( chos co chod hoaddsub hopncan eqtr3 ) ABEFBGFABGFBEFAABBCDDHABCDIJ $.
      $( [28-Mar-2006] $) $( [11-Mar-2006] $)

    $( If the difference between two operators is zero, they are equal. $)
    hosubeq0 $p |- ( ( T -op U ) = 0hop <-> T = U ) $=
      ( chod co ch0o wceq c1 cneg chot chos honegsub eqeq1i opreq1 sylbir cc
      wcel chil wf ax1cn negcl homulclt mp2an hoadd23 hoaddass hodid eqtr
      opreq2i hoaddid1 3eqtr ho0f hoaddcom 3eqtr3g syl6eq impbi ) ABEFZGHZABHZU
      RAIJZBKFZLFZBLFZGBLFZABURVBGHVCVDHVBUQGABCDMNVBGBLOPVCABLFVALFABVALFZLFZA
      AVABCUTQRSSBTSSVATIUAUBDUTBUCUDZDUEABVACDVGUFVFAGLFAVEGALVEBBEFZGBBDDMBDU
      GZUHUIACUJUHUKVDBGLFBGBULDUMBDUJUHUNUSUQVHGABBEOVIUOUP $.
      $( [31-Jul-2007] $) $( [27-Jul-2006] $)
  $}

  ${
    honpncan.1 $e |- R : H~ --> H~ $.
    honpncan.2 $e |- S : H~ --> H~ $.
    honpncan.3 $e |- T : H~ --> H~ $.
    $( Hilbert space operator cancellation law. $)
    honpncan $p |- ( ( R -op S ) +op ( S -op T ) ) = ( R -op T ) $=
      ( chod co chos hosubcl hoaddsubass honpcan opreq1i eqtr3 ) ABGHZBIHZCGHOB
      CGHIHACGHOBCABDEJEFKPACGABDELMN $.
      $( [30-Apr-2006] $) $( [11-Mar-2006] $)
  $}

  ${
    $d x y T $.
    ho0.1 $e |- T : H~ --> H~ $.
    $( A condition implying that a Hilbert space operator is identically zero.
       Lemma 3.2(S8) of [Beran] p. 95. $)
    ho01 $p |- ( A. x e. H~ A. y e. H~ ( ( T ` x ) .ih y ) = 0 <->
               T = 0hop ) $=
      ( chil c0v csn cxp wceq cv cfv wral ch0o csp co cc0 wfn wa wb wf ffn
      ax-mp ax-hv0cl elisseti fconst eqfnfv mp2an eqid mpbiran c0h df0op2
      df-ch0 xpeq2 eqtr eqeq2i wcel ffvelrni hial0 syl fvconst2 eqeq2d bitr4d
      ralbiia 3bitr4r ) CEFGZHZIZAJZCKZVHVFKZIZAELZCMIVIBJNOPIBELZAELVGEEIZVLCE
      QZVFEQZVGVNVLRSEECTVODEECUAUBEVEVFTVPEFFEUCUDZUEEVEVFUAUBAEECVFUFUGEUHUIM
      VFCMEUJHZVFUKUJVEIVRVFIULUJVEEUMUBUNUOVMVKAEVHEUPZVMVIFIZVKVSVIEUPVMVTSEE
      VHCDUQBVIURUSVSVJFVIEFVHVQUTVAVBVCVD $.
      $( [31-Jul-2007] $) $( [28-Jan-2006] $)

    $( A condition implying that a Hilbert space operator is identically zero.
       Lemma 3.2(S10) of [Beran] p. 95. $)
    ho02 $p |- ( A. x e. H~ A. y e. H~ ( x .ih ( T ` y ) ) = 0 <->
               T = 0hop ) $=
      ( cv cfv csp co cc0 wceq chil wral ch0o ralcom wcel wb ffvelrni c0v
      hial02 hial0 bitr4d syl ralbiia ho01 3bitr ) AEZBEZCFZGHIJZBKLAKLUIAKLZBK
      LUHUFGHIJAKLZBKLCMJUIABKKNUJUKBKUGKOUHKOZUJUKPKKUGCDQULUJUHRJUKAUHSAUHTUA
      UBUCBACDUDUE $.
      $( [?] $) $( [28-Jan-2006] $)
  $}

  ${
    $d x y z S $.  $d u v x y z T $.
    $( A condition implying that two Hilbert space operators are equal.
       Lemma 3.2(S9) of [Beran] p. 95. $)
    hoeq1t $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ ) -> ( A. x e. H~
          A. y e. H~ ( ( S ` x ) .ih y ) = ( ( T ` x ) .ih y ) <-> S = T ) ) $=
      ( chil wf wa cv cfv csp co wceq wral wcel wb hial2eqt ffvelrn syl2an
      anandirs ralbidva wfn eqfnfv eqid biantrur syl6bbr ffn bitr4d ) EECFZEEDF
      ZGZAHZCIZBHZJKUKDIZUMJKLBEMZAEMULUNLZAEMZCDLZUJUOUPAEUHUIUKENZUOUPOZULENU
      NENUTUHUSGUIUSGBULUNPEEUKCQEEUKDQRSTCEUAZDEUAZURUQOUHUIVAVBGUREELZUQGUQAE
      ECDUBVCUQEUCUDUEEECUFEEDUFRUG $.
      $( [21-Feb-2006] $) $( [15-Feb-2006] $)

    $( A condition implying that two Hilbert space operators are equal.
       Lemma 3.2(S11) of [Beran] p. 95. $)
    hoeq2t $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ ) -> ( A. x e. H~
          A. y e. H~ ( x .ih ( S ` y ) ) = ( x .ih ( T ` y ) ) <-> S = T ) ) $=
      ( chil wf wa cv cfv csp co wceq wral wb ralcom a1i wcel hial2eq2t
      hial2eqt bitr4d ffvelrn syl2an anandirs ralbidva hoeq1t 3bitrd ) EECFZEED
      FZGZAHZBHZCIZJKUJUKDIZJKLZBEMAEMZUNAEMZBEMZULUJJKUMUJJKLAEMZBEMCDLUOUQNUI
      UNABEEOPUIUPURBEUGUHUKEQZUPURNZULEQZUMEQZUTUGUSGUHUSGVAVBGUPULUMLURAULUMR
      AULUMSTEEUKCUAEEUKDUAUBUCUDBACDUEUF $.
      $( [26-May-2006] $) $( [15-Feb-2006] $)

    $( Every Hilbert space operator has at most one adjoint. $)
    adjmo $p |- E* u ( u : H~ --> H~ /\
           A. x e. H~ A. y e. H~ ( x .ih ( T ` y ) ) = ( ( u ` x ) .ih y ) ) $=
      ( vv chil cv wf cfv csp co wceq wral wa wmo wi wal hoeq1t biimpa
      r19.26-2 eqtr2t r19.20si sylbir sylan2 an4s gen2 feq1 fveq1 opreq1d
      eqeq2d 2ralbidv anbi12d mo4 mpbir ) FFCGZHZAGZBGZDIJKZUQUOIZURJKZLZBFMAFM
      ZNZCOVDFFEGZHZUSUQVEIZURJKZLZBFMAFMZNZNUOVELZPZEQCQVMCEUPVFVCVJVLUPVFNZVA
      VHLZBFMZAFMZVLVCVJNZVNVQVLABUOVERSVRVBVINZBFMZAFMVQVBVIABFFTVTVPAFVSVOBFU
      SVAVHUAUBUBUCUDUEUFVDVKCEVLUPVFVCVJFFUOVEUGVLVBVIABFFVLVAVHUSVLUTVGURJUQU
      OVEUHUIUJUKULUMUN $.
      $( [21-Feb-2006] $) $( [18-Feb-2006] $)

    $( Symmetry property of an adjoint. $)
    adjsymt $p |- ( ( S : H~ --> H~ /\ T : H~ --> H~ ) ->
          ( A. x e. H~ A. y e. H~ ( x .ih ( S ` y ) ) = ( ( T ` x ) .ih y ) <->
         A. x e. H~ A. y e. H~ ( x .ih ( T ` y ) ) = ( ( S ` x ) .ih y ) ) ) $=
      ( vz chil wf wa cv cfv csp co wceq wral wcel wb ccj ax-his1 ffvelrn
      sylan adantrl sylan2 adantll eqeq12d ancoms cc cj11t hiclt sylanc bitr2d
      an4s anassrs eqcom syl6bb ralbidva ralcom fveq2 opreq2d opreq2 ralbidv
      cbvralv bitr4 opreq1 opreq1d ralbii 3bitr syl6rbbr ) FFCGZFFDGZHZAIZBIZDJ
      ZKLZVKCJZVLKLZMZBFNZAFNVLVOKLZVMVKKLZMZBFNZAFNZVKVLCJZKLZVKDJZVLKLZMZBFNA
      FNZVJVRWBAFVJVKFOZHZVQWABFWKVLFOZHVQVTVSMZWAVJWJWLVQWMPZVHWJVIWLWNVHWJHZV
      IWLHZHZWMVNQJZVPQJZMZVQWPWOWMWTPWPWOHVTWRVSWSWPWJVTWRMZVHVMFOZWJXAWPVMVKR
      FFVLDSZTUAWLWOVSWSMZVIWLVOFOZXDWOVLVORFFVKCSZUBUCUDUEVNUFOZVPUFOZWTVQPWQV
      NVPUGWJWPXGVHWJXBXGWPVKVMUHXCUBUCWOWLXHVIXEWLXHWOVOVLUHXFTUAUIUJUKULVTVSU
      MUNUOUOWIVKEIZCJZKLZWFXIKLZMZAFNZEFNZVLXJKLZVMXIKLZMZBFNZEFNWCWIWHAFNZBFN
      XOWHABFFUPXNXTEBFXIVLMZXMWHAFYAXKWEXLWGYAXJWDVKKXIVLCUQURXIVLWFKUSUDUTVAV
      BXNXSEFXMXRABFVKVLMZXKXPXLXQVKVLXJKVCYBWFVMXIKVKVLDUQVDUDVAVEXSWBEAFXIVKM
      ZXRWABFYCXPVSXQVTYCXJVOVLKXIVKCUQURXIVKVMKUSUDUTVAVFVG $.
      $( [25-Feb-2006] $) $( [18-Feb-2006] $)
  $}

  ${
    eigre.1 $e |- A e. H~ $.
    eigre.2 $e |- B e. CC $.
    $( A necessary and sufficient condition (that holds when ` T ` is a
       Hermitian operator) for an eigenvalue ` B ` to be real.  Generalization
       of Equation 1.30 of [Hughes] p. 49. $)
    eigre $p |- ( ( ( T ` A ) = ( B .h A ) /\ A =/= 0h ) ->
                 ( ( A .ih ( T ` A ) ) = ( ( T ` A ) .ih A ) <-> B e. RR ) ) $=
      ( cfv csm co wceq c0v wne wa csp ccj cr wcel cmul opreq2 cc chil his5t
      mp3an syl6eq opreq1 ax-his3 eqeq12d cc0 clt wbr wb ax-his4 mpan hiidrclt
      ax-mp gt0ne0 w3a cjcl hicl 3pm3.2i mulcan2t 3syl sylan9bb cjreb syl6bbr
      ) ACFZBAGHZIZAJKZLAVEMHZVEAMHZIZBNFZBIZBOPVGVKVLAAMHZQHZBVNQHZIZVHVMVGVIV
      OVJVPVGVIAVFMHZVOVEVFAMRBSPZATPZVTVRVOIEDDBAAUAUBUCVGVJVFAMHZVPVEVFAMUDVS
      VTVTWAVPIEDDBAAUEUBUCUFVHUGVNUHUIZVNUGKZVQVMUJZVTVHWBDAUKULVNVTVNOPDAUMUN
      UOVLSPZVSVNSPZUPWCWDWEVSWFBEUQEAADDURUSVLBVNUTULVAVBBEVCVD $.
      $( [18-May-2006] $) $( [21-Jan-2005] $)
  $}

  $( A necessary and sufficient condition (that holds when ` T ` is a Hermitian
     operator) for an eigenvalue ` B ` to be real.  Generalization of Equation
     1.30 of [Hughes] p. 49. $)
  eigret $p |- ( ( ( A e. H~ /\ B e. CC ) /\ ( ( T ` A ) = ( B .h A ) /\ A =/=
       0h ) ) -> ( ( A .ih ( T ` A ) ) = ( ( T ` A ) .ih A ) <-> B e. RR ) ) $=
    ( chil wcel cc wa cfv csm co wceq c0v wne csp cr wb wi cif cc0 fveq2
    opreq2 eqeq12d neeq1 anbi12d id opreq12d bibi1d imbi12d opreq1 eqeq2d
    anbi1d eleq1 bibi2d ax-hv0cl elimel 0cn eigre dedth2h imp ) ADEZBFEZGACHZBA
    IJZKZALMZGZAVBNJZVBANJZKZBOEZPZUTVAVFVKQUTALRZCHZBVLIJZKZVLLMZGZVLVMNJZVMVL
    NJZKZVJPZQVMVABSRZVLIJZKZVPGZVTWBOEZPZQABLSAVLKZVFVQVKWAWHVDVOVEVPWHVBVMVCV
    NAVLCTZAVLBIUAUBAVLLUCUDWHVIVTVJWHVGVRVHVSWHAVLVBVMNWHUEZWIUFWHVBVMAVLNWIWJ
    UFUBUGUHBWBKZVQWEWAWGWKVOWDVPWKVNWCVMBWBVLIUIUJUKWKVJWFVTBWBOULUMUHVLWBCALD
    UNUOBSFUPUOUQURUS $.
    $( [26-May-2006] $) $( [19-Mar-2006] $)

  ${
    eigpos.1 $e |- A e. H~ $.
    eigpos.2 $e |- B e. CC $.
    $( A sufficient condition (first conjunct pair, that holds when ` T ` is a
       positive operator) for an eigenvalue ` B ` (second conjunct pair) to be
       nonnegative.  Remark (ii) in [Hughes] p. 137. $)
    eigpos $p |- ( ( ( ( A .ih ( T ` A ) ) e. RR /\ 0 <_ ( A .ih ( T ` A ) ) )
      /\ ( ( T ` A ) = ( B .h A ) /\ A =/= 0h ) ) -> ( B e. RR /\ 0 <_ B ) ) $=
      ( cfv csp co cr wcel cc0 cle wbr wa csm wceq c0v wne wb chil hvmulcl
      hiret mp2an a1i opreq2 eleq1d opreq1 eqeq12d 3bitr4d adantr eigre bitrd
      biimpac adantlr clt cmul hiidrclt ax-mp prodge02t mpanl2 ax-his4 mpan
      ad2antll simplr ad2antrl ccj cjreb sylib opreq1d cc his5t mp3an syl5eq
      eqtrd breqtrd jca sylanc ) AACFZGHZIJZKVSLMZNZVRBAOHZPZAQRZNZNZBIJZKBLMZV
      TWFWHWAWFVTWHWFVTVSVRAGHZPZWHWDVTWKSWEWDAWCGHZIJZWLWCAGHZPZVTWKWMWOSZWDAT
      JZWCTJWPDBAEDUAAWCUBUCUDWDVSWLIVRWCAGUEZUFWDVSWLWJWNWRVRWCAGUGUHUIUJABCDE
      UKULUMUNZWHKAAGHZUOMZKBWTUPHZLMZNZWIWGWHWTIJZXDWIWQXEDAUQURBWTUSUTWSWGXAX
      CWEXAWBWDWQWEXADAVAVBVCWGKVSXBLVTWAWFVDWGVSWLXBWDVSWLPWBWEWRVEWGBVFFZWTUP
      HZXBWLWGXFBWTUPWGWHXFBPWSBEVGVHVIBVJJWQWQWLXGPEDDBAAVKVLVMVNVOVPVQVP $.
      $( [13-Oct-2006] $) $( [2-Jul-2005] $)
  $}

  ${
    eigorth.1 $e |- A e. H~ $.
    eigorth.2 $e |- B e. H~ $.
    eigorth.3 $e |- C e. CC $.
    eigorth.4 $e |- D e. CC $.
    $( A necessary and sufficient condition (that holds when ` T ` is a
       Hermitian operator) for two eigenvectors ` A ` and ` B ` to be
       orthogonal.  Generalization of Equation 1.31 of [Hughes] p. 49. $)
    eigorth $p |- ( ( ( ( T ` A ) = ( C .h A ) /\ ( T ` B ) = ( D .h B ) )
                     /\ -. C = ( * ` D ) ) ->
         ( ( A .ih ( T ` B ) ) = ( ( T ` A ) .ih B ) <-> ( A .ih B ) = 0 ) ) $=
      ( cfv csm co wceq wa csp ccj cmul wn cc0 opreq2 cc wcel chil his5t mp3an
      syl6eq opreq1 ax-his3 eqeqan12rd wb wi cjcl hicl w3a wne mulcan2t df-ne
      sylan2br ex eqcom syl6bb biimpcd con1d com12 mul01 eqtr4 eqtr4d impbid1
      sylan9bb ) AEJZCAKLZMZBEJZDBKLZMZNAVMOLZVJBOLZMDPJZABOLZQLZCVSQLZMZCVRMZR
      ZVSSMZVOVLVPVTVQWAVOVPAVNOLZVTVMVNAOTDUAUBAUCUBZBUCUBZWFVTMIFGDABUDUEUFVL
      VQVKBOLZWAVJVKBOUGCUAUBZWGWHWIWAMHFGCABUHUEUFUIWDWBWEWBWDWEWBWEWCWERZWBWC
      WKWBVRCMZWCVRUAUBZWJVSUAUBZWKWBWLUJZUKDIULZHABFGUMWMWJWNUNZWKWOWQVSSUOWOW
      KVRCVSUPVSSUQURUSUEVRCUTVAVBVCVDWEVTVRSQLZWAVSSVRQTWEWACSQLZWRVSSCQTWSSWR
      CHVEVRWPVEVFUFVGVHVI $.
      $( [30-Jan-2005] $) $( [23-Jan-2005] $)
  $}

  $( A necessary and sufficient condition (that holds when ` T ` is a
     Hermitian operator) for two eigenvectors ` A ` and ` B ` to be
     orthogonal.  Generalization of Equation 1.31 of [Hughes] p. 49. $)
  eigortht $p |- ( ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. CC /\ D e. CC ) ) /\
                 ( ( ( T ` A ) = ( C .h A ) /\ ( T ` B ) = ( D .h B ) )
                   /\ -. C = ( * ` D ) ) ) ->
         ( ( A .ih ( T ` B ) ) = ( ( T ` A ) .ih B ) <-> ( A .ih B ) = 0 ) ) $=
    ( chil wcel wa cc cfv csm co wceq ccj wn csp cc0 wb wi c0v cif fveq2
    opreq2 eqeq12d anbi1d opreq1 opreq1d eqeq1d bibi12d imbi12d anbi2d opreq2d
    eqeq2d eqeq1 negbid anbi12d imbi1d ax-hv0cl elimel 0cn eigorth dedth4h imp
    ) AFGZBFGZHCIGZDIGZHHAEJZCAKLZMZBEJZDBKLZMZHZCDNJZMZOZHZAVKPLZVHBPLZMZABPLZ
    QMZRZVDVEVFVGVRWDSABCDTTVDATUAZEJZCWEKLZMZVMHZVQHZWEVKPLZWFBPLZMZWEBPLZQMZR
    ZSWHVEBTUAZEJZDWQKLZMZHZVQHZWEWRPLZWFWQPLZMZWEWQPLZQMZRZSWFVFCQUAZWEKLZMZWT
    HZXIVOMZOZHZXHSXKWRVGDQUAZWQKLZMZHZXIXPNJZMZOZHZXHSQQAWEMZVRWJWDWPYDVNWIVQY
    DVJWHVMYDVHWFVIWGAWEEUBZAWECKUCUDUEUEYDWAWMWCWOYDVSWKVTWLAWEVKPUFYDVHWFBPYE
    UGUDYDWBWNQAWEBPUFUHUIUJBWQMZWJXBWPXHYFWIXAVQYFVMWTWHYFVKWRVLWSBWQEUBZBWQDK
    UCUDUKUEYFWMXEWOXGYFWKXCWLXDYFVKWRWEPYGULBWQWFPUCUDYFWNXFQBWQWEPUCUHUIUJCXI
    MZXBXOXHYHXAXLVQXNYHWHXKWTYHWGXJWFCXIWEKUFUMUEYHVPXMCXIVOUNUOUPUQDXPMZXOYCX
    HYIXLXSXNYBYIWTXRXKYIWSXQWRDXPWQKUFUMUKYIXMYAYIVOXTXIDXPNUBUMUOUPUQWEWQXIXP
    EATFURUSBTFURUSCQIUTUSDQIUTUSVAVBVC $.
    $( [23-May-2006] $) $( [23-Mar-2006] $)

