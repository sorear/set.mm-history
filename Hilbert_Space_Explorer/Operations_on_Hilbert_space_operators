$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Operations on Hilbert space operators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A $.  $d x y S $.  $d x y T $.
    $( The sum of Hilbert space operators is an operator. $)
    hoaddcl $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( S +op T ) : ~H --> ~H ) $=
      ( vx vy chil wf wa chos co cv wcel cfv cva wceq copab wral hvaddcl
      ffvelrn syl2an anandirs r19.21aiva eqid fopab2 sylib hosmval feq1d
      mpbird ) EEAFZEEBFZGZEEABHIZFEECJZEKZDJULALZULBLZMIZNGCDOZFZUJUPEKZCEPURU
      JUSCEUHUIUMUSUNEKUOEKUSUHUMGUIUMGUNUOQEEULAREEULBRSTUACDEEUPUQUQUBUCUDUJE
      EUKUQCDABUEUFUG $.
      $( [22-Feb-2006] $) $( [21-Feb-2006] $)

    $( The scalar product of a Hilbert space operator is an operator. $)
    homulcl $p |- ( ( A e. CC /\ T : ~H --> ~H ) ->
                   ( A .op T ) : ~H --> ~H ) $=
      ( vx vy cc wcel chil wf wa chot co cv cfv csm wceq copab wral hvmulcl
      ffvelrn sylan2 anassrs r19.21aiva eqid fopab2 sylib hommval feq1d mpbird
      ) AEFZGGBHZIZGGABJKZHGGCLZGFZDLAUMBMZNKZOICDPZHZUKUPGFZCGQURUKUSCGUIUJUNU
      SUIUOGFUSUJUNIAUORGGUMBSTUAUBCDGGUPUQUQUCUDUEUKGGULUQCDABUFUGUH $.
      $( [22-Mar-2006] $) $( [21-Feb-2006] $)
  $}

  ${
    $d x T $.  $d x U $.
    $( Equality of Hilbert space operators. $)
    hoeq $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                ( A. x e. ~H ( T ` x ) = ( U ` x ) <-> T = U ) ) $=
      ( chil wfn cv cfv wceq wral wb wf wa eqfnfv2 bicomd ffn syl2an ) BDEZCDEZ
      AFZBGSCGHADIZBCHZJDDBKDDCKQRLUATADBCMNDDBODDCOP $.
      $( [13-Aug-2006] $) $( [12-Aug-2006] $)
  $}

  ${
    $d x y S $.  $d x y T $.
    hoeq.1 $e |- S : ~H --> ~H $.
    hoeq.2 $e |- T : ~H --> ~H $.
    $( Equality of Hilbert space operators. $)
    hoeqi $p |- ( A. x e. ~H ( S ` x ) = ( T ` x ) <-> S = T ) $=
      ( chil wf cv cfv wceq wral wb hoeq mp2an ) FFBGFFCGAHZBIOCIJAFKBCJLDEABCM
      N $.
      $( [14-Nov-2000] $)

    $( Closure of Hilbert space operator sum. $)
    hoscli $p |- ( A e. ~H -> ( ( S +op T ) ` A ) e. ~H ) $=
      ( chil wf wcel chos co cfv hoscl mpanl12 ) FFBGFFCGAFHABCIJKFHDEABCLM $.
      $( [12-Nov-2000] $)

    $( Closure of Hilbert space operator difference. $)
    hodcli $p |- ( A e. ~H -> ( ( S -op T ) ` A ) e. ~H ) $=
      ( chil wf wcel chod co cfv hodcl mpanl12 ) FFBGFFCGAFHABCIJKFHDEABCLM $.
      $( [12-Nov-2000] $)

    $( Composition of Hilbert space operators. $)
    hocoi $p |- ( A e. ~H -> ( ( S o. T ) ` A ) = ( S ` ( T ` A ) ) ) $=
      ( chil wcel cdm ccom cfv wceq fdmi eleq2i wfun wf ffun ax-mp fvco
      mp3an12 sylbir ) AFGACHZGZABCIJACJBJKZUAFAFFCELMBNZCNZUBUCFFBOUDDFFBPQFFC
      OUEEFFCPQABCRST $.
      $( [12-Nov-2000] $)

    $( Closure of composition of Hilbert space operators. $)
    hococli $p |- ( A e. ~H -> ( ( S o. T ) ` A ) e. ~H ) $=
      ( chil wcel ccom cfv hocoi ffvelrni syl eqeltrd ) AFGZABCHIACIZBIZFABCDEJ
      NOFGPFGFFACEKFFOBDKLM $.
      $( [12-Nov-2000] $)

    $( Mapping of composition of Hilbert space operators. $)
    hocofi $p |- ( S o. T ) : ~H --> ~H $=
      ( chil wf ccom fco mp2an ) EEAFEEBFEEABGFCDEEEABHI $.
      $( [14-Nov-2000] $)

    $( Functionality of composition of Hilbert space operators. $)
    hocofni $p |- ( S o. T ) Fn ~H $=
      ( chil ccom wf wfn hocofi ffn ax-mp ) EEABFZGLEHABCDIEELJK $.
      $( [12-Nov-2000] $)

    $( Mapping of sum of Hilbert space operators. $)
    hoaddcli $p |- ( S +op T ) : ~H --> ~H $=
      ( chil wf chos co hoaddcl mp2an ) EEAFEEBFEEABGHFCDABIJ $.
      $( [14-Nov-2000] $)

    $( Mapping of difference of Hilbert space operators. $)
    hosubcli $p |- ( S -op T ) : ~H --> ~H $=
      ( vx vy chil cv cfv cmv co chod wf wcel wceq wa copab hodmval mp2an
      hvsubcl ffvelrni sylanc fopab ) EFGGEHZAIZUDBIZJKZABLKZGGAMGGBMUHUDGNZFHU
      GOPEFQOCDEFABRSUEGNUFGNUGGNUIUEUFTGGUDACUAGGUDBDUAUBUC $.
      $( [14-Nov-2000] $)

    $( Functionality of sum of Hilbert space operators. $)
    hoaddfni $p |- ( S +op T ) Fn ~H $=
      ( chil chos co wf wfn hoaddcli ffn ax-mp ) EEABFGZHMEIABCDJEEMKL $.
      $( [14-Nov-2000] $)

    $( Functionality of difference of Hilbert space operators. $)
    hosubfni $p |- ( S -op T ) Fn ~H $=
      ( chil chod co wf wfn hosubcli ffn ax-mp ) EEABFGZHMEIABCDJEEMKL $.
      $( [30-Jun-2006] $) $( [2-Jun-2006] $)

    $( Commutativity of sum of Hilbert space operators. $)
    hoaddcomi $p |- ( S +op T ) = ( T +op S ) $=
      ( vx cv chos co cfv wceq chil wral wcel cva ax-hvcom ffvelrni sylanc wf
      hosvalOLD mpanl12 3eqtr4d rgen hoaddcli hoeqi mpbi ) EFZABGHZIZUFBAGHZIZJ
      ZEKLUGUIJUKEKUFKMZUFAIZUFBIZNHZUNUMNHZUHUJUMKMUNKMUOUPJULUMUNOKKUFACPKKUF
      BDPQKKARZKKBRZULUHUOJCDUFABSTURUQULUJUPJDCUFBASTUAUBEUGUIABCDUCBADCUCUDUE
      $.
      $( [15-Nov-2000] $)
  $}

  $( Mapping of difference of Hilbert space operators. $)
  hosubcl $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
              ( S -op T ) : ~H --> ~H ) $=
    ( chil wf chod co ch0o cif wceq opreq1 feq1d opreq2 ho0f elimf hosubcli
    dedth2h ) CCADZCCBDZCCABEFZDCCQAGHZBEFZDCCTRBGHZEFZDABGGATICCSUAATBEJKBUBIC
    CUAUCBUBTELKTUBCCAGMNCCBGMNOP $.
    $( [25-Aug-2006] $) $( [23-Aug-2006] $)

  $( Commutativity of sum of Hilbert space operators. $)
  hoaddcom $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( S +op T ) = ( T +op S ) ) $=
    ( chil wf chos co wceq ch0o cif opreq1 opreq2 eqeq12d ho0f elimf hoaddcomi
    dedth2h ) CCADZCCBDZABEFZBAEFZGQAHIZBEFZBUAEFZGUARBHIZEFZUDUAEFZGABHHAUAGSU
    BTUCAUABEJAUABEKLBUDGUBUEUCUFBUDUAEKBUDUAEJLUAUDCCAHMNCCBHMNOP $.
    $( [26-Aug-2006] $) $( [24-Aug-2006] $)

  ${
    $d x R $.  $d x S $.  $d x T $.
    hods.1 $e |- R : ~H --> ~H $.
    hods.2 $e |- S : ~H --> ~H $.
    hods.3 $e |- T : ~H --> ~H $.
    $( Relationship between Hilbert space operator difference and sum. $)
    hodsi $p |- ( ( R -op S ) = T <-> ( S +op T ) = R ) $=
      ( vx cv chod co cfv wceq chil wral chos wcel cmv cva wb hvsubadd
      ffvelrni syl3anc wf hodvalOLD mpanl12 eqeq1d hosvalOLD 3bitr4d ralbiia
      hosubcli hoeqi hoaddcli 3bitr3i ) GHZABIJZKZUNCKZLZGMNUNBCOJZKZUNAKZLZGMN
      UOCLUSALURVBGMUNMPZVAUNBKZQJZUQLZVDUQRJZVALZURVBVAMPVDMPUQMPVFVHSVCVAVDUQ
      TMMUNADUAMMUNBEUAMMUNCFUAUBVCUPVEUQMMAUCMMBUCZVCUPVELDEUNABUDUEUFVCUTVGVA
      VIMMCUCVCUTVGLEFUNBCUGUEUFUHUIGUOCABDEUJFUKGUSABCEFULDUKUM $.
      $( [17-Nov-2000] $)

    $( Associativity of sum of Hilbert space operators. $)
    hoaddassi $p |- ( ( R +op S ) +op T ) = ( R +op ( S +op T ) ) $=
      ( vx cv chos co cfv wceq chil wral wcel cva ax-hvass ffvelrni syl3anc wf
      hoaddcli hosvalOLD mpanl12 opreq1d eqtrd opreq2d 3eqtr4d rgen hoeqi mpbi
      ) GHZABIJZCIJZKZUKABCIJZIJZKZLZGMNUMUPLURGMUKMOZUKAKZUKBKZPJZUKCKZPJZUTVA
      VCPJZPJZUNUQUTMOVAMOVCMOVDVFLUSUTVAVCQMMUKADRMMUKBERMMUKCFRSUSUNUKULKZVCP
      JZVDMMULTMMCTZUSUNVHLABDEUAZFUKULCUBUCUSVGVBVCPMMATZMMBTZUSVGVBLDEUKABUBU
      CUDUEUSUQUTUKUOKZPJZVFVKMMUOTUSUQVNLDBCEFUAZUKAUOUBUCUSVMVEUTPVLVIUSVMVEL
      EFUKBCUBUCUFUEUGUHGUMUPULCVJFUAAUODVOUAUIUJ $.
      $( [26-Nov-2000] $)

    $( Commutative/associative law for Hilbert space operator sum that swaps
       the first two terms. $)
    hoadd12i $p |- ( R +op ( S +op T ) ) = ( S +op ( R +op T ) ) $=
      ( chos co hoaddcomi opreq1i hoaddassi 3eqtr3i ) ABGHZCGHBAGHZCGHABCGHGHBA
      CGHGHMNCGABDEIJABCDEFKBACEDFKL $.
      $( [21-Sep-2004] $) $( [27-Aug-2004] $)

    $( Commutative/associative law for Hilbert space operator sum that swaps
       the second and third terms. $)
    hoadd23i $p |- ( ( R +op S ) +op T ) = ( ( R +op T ) +op S ) $=
      ( chos co hoaddcomi opreq2i hoaddassi 3eqtr4i ) ABCGHZGHACBGHZGHABGHCGHAC
      GHBGHMNAGBCEFIJABCDEFKACBDFEKL $.
      $( [28-Jul-2006] $) $( [27-Jul-2006] $)

    $( Distributive law for Hilbert space operator sum. $)
    hocadddiri $p |- ( ( R +op S ) o. T ) = ( ( R o. T ) +op ( S o. T ) ) $=
      ( vx cv chos co ccom cfv wceq chil wral wcel cva ffvelrni wf hosvalOLD
      mpanl12 syl hocoi opreq12d eqtr4d hoaddcli hocofi 3eqtr4d rgen hoeqi
      mpbi ) GHZABIJZCKZLZULACKZBCKZIJZLZMZGNOUNURMUTGNULNPZULCLZUMLZULUPLZULUQ
      LZQJZUOUSVAVCVBALZVBBLZQJZVFVAVBNPZVCVIMZNNULCFRNNASNNBSVJVKDEVBABTUAUBVA
      VDVGVEVHQULACDFUCULBCEFUCUDUEULUMCABDEUFZFUCNNUPSNNUQSVAUSVFMACDFUGZBCEFU
      GZULUPUQTUAUHUIGUNURUMCVLFUGUPUQVMVNUFUJUK $.
      $( [26-Nov-2000] $)

    $( Distributive law for Hilbert space operator difference. $)
    hocsubdiri $p |- ( ( R -op S ) o. T ) = ( ( R o. T ) -op ( S o. T ) ) $=
      ( vx cv chod co ccom cfv wceq chil wral wcel cmv ffvelrni wf hodvalOLD
      mpanl12 syl hocoi opreq12d eqtr4d hosubcli hocofi 3eqtr4d rgen hoeqi
      mpbi ) GHZABIJZCKZLZULACKZBCKZIJZLZMZGNOUNURMUTGNULNPZULCLZUMLZULUPLZULUQ
      LZQJZUOUSVAVCVBALZVBBLZQJZVFVAVBNPZVCVIMZNNULCFRNNASNNBSVJVKDEVBABTUAUBVA
      VDVGVEVHQULACDFUCULBCEFUCUDUEULUMCABDEUFZFUCNNUPSNNUQSVAUSVFMACDFUGZBCEFU
      GZULUPUQTUAUHUIGUNURUMCVLFUGUPUQVMVNUFUJUK $.
      $( [26-Nov-2000] $)

    $( Double composition of Hilbert space operators. $)
    ho2coi $p |- ( A e. ~H -> ( ( ( R o. S ) o. T ) ` A ) =
                 ( R ` ( S ` ( T ` A ) ) ) ) $=
      ( chil wcel ccom cfv hocofi hocoi wceq ffvelrni syl eqtrd ) AHIZABCJZDJKA
      DKZSKZTCKBKZASDBCEFLGMRTHIUAUBNHHADGOTBCEFMPQ $.
      $( [1-Dec-2000] $)
  $}

  $( Associativity of sum of Hilbert space operators. $)
  hoaddass $p |- ( ( R : ~H --> ~H /\ S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( ( R +op S ) +op T ) = ( R +op ( S +op T ) ) ) $=
    ( chil wf chos co wceq ch0o cif opreq1 opreq1d eqeq12d opreq2 opreq2d ho0f
    elimf hoaddassi dedth3h ) DDAEZDDBEZDDCEZABFGZCFGZABCFGZFGZHTAIJZBFGZCFGZUG
    UEFGZHUGUABIJZFGZCFGZUGUKCFGZFGZHULUBCIJZFGZUGUKUPFGZFGZHABCIIIAUGHZUDUIUFU
    JUTUCUHCFAUGBFKLAUGUEFKMBUKHZUIUMUJUOVAUHULCFBUKUGFNLVAUEUNUGFBUKCFKOMCUPHZ
    UMUQUOUSCUPULFNVBUNURUGFCUPUKFNOMUGUKUPDDAIPQDDBIPQDDCIPQRS $.
    $( [25-Aug-2006] $) $( [24-Aug-2006] $)

  $( Commutative/associative law for Hilbert space operator sum that swaps the
     second and third terms. $)
  hoadd23 $p |- ( ( R : ~H --> ~H /\ S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( ( R +op S ) +op T ) = ( ( R +op T ) +op S ) ) $=
    ( chil wf w3a chos co wceq hoaddcom 3adant1 opreq2d hoaddass 3com23
    3eqtr4d ) DDAEZDDBEZDDCEZFZABCGHZGHACBGHZGHZABGHCGHACGHBGHZSTUAAGQRTUAIPBCJ
    KLABCMPRQUCUBIACBMNO $.
    $( [26-Aug-2006] $) $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a sum of Hilbert space operators. $)
  hoadd4 $p |- ( ( ( R : ~H --> ~H /\ S : ~H --> ~H ) /\
      ( T : ~H --> ~H /\ U : ~H --> ~H ) ) -> ( ( R +op S ) +op ( T +op U ) ) =
                ( ( R +op T ) +op ( S +op U ) ) ) $=
    ( chil wf wa chos co wceq w3a hoadd23 opreq1d 3expa adantrr hoaddass 3expb
    hoaddcl sylan an4s 3eqtr3d ) EEAFZEEBFZGZEECFZEEDFZGZGABHIZCHIZDHIZACHIZBHI
    ZDHIZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOEEUHFZUGUJUN
    JZUDUQUEUFURUHCDPQABRSUBUEUCUFUMUOJZEEUKFZUCUFGUSUBUEGUTUCUFUSUKBDPQACRSTUA
    $.
    $( [27-Aug-2006] $) $( [24-Aug-2006] $)

  $( Distributive law for Hilbert space operator difference. $)
  hocsubdir $p |- ( ( R : ~H --> ~H /\ S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( ( R -op S ) o. T ) = ( ( R o. T ) -op ( S o. T ) ) ) $=
    ( chil wf chod co ccom wceq ch0o cif opreq1 coeq1d coeq1 opreq1d eqeq12d
    opreq2 opreq2d coeq2 opreq12d ho0f elimf hocsubdiri dedth3h ) DDAEZDDBEZDDC
    EZABFGZCHZACHZBCHZFGZIUEAJKZBFGZCHZUMCHZUKFGZIUMUFBJKZFGZCHZUPURCHZFGZIUSUG
    CJKZHZUMVCHZURVCHZFGZIABCJJJAUMIZUIUOULUQVHUHUNCAUMBFLMVHUJUPUKFAUMCNOPBURI
    ZUOUTUQVBVIUNUSCBURUMFQMVIUKVAUPFBURCNRPCVCIZUTVDVBVGCVCUSSVJUPVEVAVFFCVCUM
    SCVCURSTPUMURVCDDAJUAUBDDBJUAUBDDCJUAUBUCUD $.
    $( [27-Aug-2006] $) $( [23-Aug-2006] $)

  ${
    $d x T $.
    hoaddid1.1 $e |- T : ~H --> ~H $.
    $( Sum of a Hilbert space operator with the zero operator. $)
    hoaddid1i $p |- ( T +op 0hop ) = T $=
      ( vx cv ch0o chos co cfv wceq chil wral wcel cva c0v wf ho0f hosval
      mp3an12 ho0val opreq2d ffvelrni ax-hvaddid syl 3eqtrd rgen hoaddcli
      hoeqi mpbi ) CDZAEFGZHZUIAHZIZCJKUJAIUMCJUIJLZUKULUIEHZMGZULNMGZULJJAOJJE
      OUNUKUPIBPUIAEQRUNUONULMUISTUNULJLUQULIJJUIABUAULUBUCUDUECUJAAEBPUFBUGUH
      $.
      $( [13-Aug-2006] $) $( [15-Nov-2000] $)

    $( Difference of a Hilbert space operator from itself. $)
    hodidi $p |- ( T -op T ) = 0hop $=
      ( chod co ch0o wceq chos hoaddid1i ho0f hodsi mpbir ) AACDEFAEGDAFABHAAEB
      BIJK $.
      $( [30-Jul-2007] $) $( [10-Mar-2006] $)

    $( Composition of the zero operator and a Hilbert space operator. $)
    ho0coi $p |- ( 0hop o. T ) = 0hop $=
      ( vx cv ch0o ccom cfv wceq chil wral wcel c0v ffvelrni ho0val syl ho0f
      hocoi 3eqtr4d rgen hocofi hoeqi mpbi ) CDZEAFZGZUCEGZHZCIJUDEHUGCIUCIKZUC
      AGZEGZLUEUFUHUIIKUJLHIIUCABMUINOUCEAPBQUCNRSCUDEEAPBTPUAUB $.
      $( [9-Aug-2006] $)

    $( Composition of Hilbert space operator with unit identity. $)
    hoid1i $p |- ( T o. Iop ) = T $=
      ( vx chio ccom chil cpj cfv df-iop coeq2i cv wceq wral wcel helch pjfi
      hocoi pjch1 fveq2d eqtrd rgen hocofi hoeqi mpbi eqtri ) ADEAFGHZEZADUFAIJ
      CKZUGHZUHAHZLZCFMUGALUKCFUHFNZUIUHUFHZAHUJUHAUFBFOPZQULUMUHAUHRSTUACUGAAU
      FBUNUBBUCUDUE $.
      $( [18-Sep-2007] $) $( [15-Nov-2000] $)

    $( Composition of Hilbert space operator with unit identity. $)
    hoid1ri $p |- ( Iop o. T ) = T $=
      ( vx chio ccom chil cpj cfv df-iop coeq1i cv wceq wral wcel helch pjfi
      hocoi ffvelrni pjch1 syl eqtrd rgen hocofi hoeqi mpbi eqtri ) DAEFGHZAEZA
      DUGAIJCKZUHHZUIAHZLZCFMUHALULCFUIFNZUJUKUGHZUKUIUGAFOPZBQUMUKFNUNUKLFFUIA
      BRUKSTUAUBCUHAUGAUOBUCBUDUEUF $.
      $( [17-Nov-2007] $) $( [15-Nov-2000] $)
  $}

  $( Sum of a Hilbert space operator with the zero operator. $)
  hoaddid1 $p |- ( T : ~H --> ~H -> ( T +op 0hop ) = T ) $=
    ( chil wf ch0o chos co wceq cif opreq1 id eqeq12d ho0f elimf hoaddid1i
    dedth ) BBACZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRBBADLMNO $.
    $( [31-Jul-2007] $) $( [25-Jul-2006] $)

  $( Difference of a Hilbert space operator from itself. $)
  hodid $p |- ( T : ~H --> ~H -> ( T -op T ) = 0hop ) $=
    ( chil wf chod co ch0o wceq cif id opreq12d eqeq1d ho0f elimf hodidi dedth
    ) BBACZAADEZFGPAFHZRDEZFGAFARGZQSFTARARDTIZUAJKRBBAFLMNO $.
    $( [31-Jul-2007] $) $( [23-Jul-2006] $)

  $( A Hilbert space operator is not empty. $)
  hon0 $p |- ( T : ~H --> ~H -> -. T = (/) ) $=
    ( chil wf c0 wceq c0v wcel wn ax-hv0cl n0i ax-mp wfn wi ffn fndmu ex syl
    fn0 syl5ibr mtoi ) BBACZADEZBDEZFBGUCHIBFJKUAADLZUCUBUAABLZUDUCMBBANUEUDUCB
    DAOPQARST $.
    $( [23-May-2006] $) $( [24-Mar-2006] $)

  ${
    $d x S $.  $d x T $.
    hodseq.2 $e |- S : ~H --> ~H $.
    hodseq.3 $e |- T : ~H --> ~H $.
    $( Subtraction and addition of equal Hilbert space operators. $)
    hodseqi $p |- ( S +op ( T -op S ) ) = T $=
      ( chod co wceq chos eqid hosubcli hodsi mpbi ) BAEFZMGAMHFBGMIBAMDCBADCJK
      L $.
      $( [22-Oct-2004] $) $( [27-Aug-2004] $)

    $( Subtraction of Hilbert space operators expressed in terms of difference
       from zero. $)
    ho0subi $p |- ( S -op T ) = ( S +op ( 0hop -op T ) ) $=
      ( chod co ch0o chos wceq ho0f hosubcli hoadd12i hodseqi opreq2i
      hoaddid1i 3eqtri hoaddcli hodsi mpbir ) ABEFAGBEFZHFZIBUAHFZAIUBABTHFZHFA
      GHFABATDCGBJDKZLUCGAHBGDJMNACOPABUACDATCUDQRS $.
      $( [13-Aug-2006] $) $( [10-Mar-2006] $)

    $( Relationship between Hilbert operator addition and subtraction. $)
    honegsubi $p |- ( S +op ( -u 1 .op T ) ) = ( S -op T ) $=
      ( vx cv c1 cneg chot co chos cfv chod wceq chil wral wcel cmv cva csm
      hvsubval ffvelrni sylanc cc wf ax1cn negcli homval mp3an12 opreq2d
      eqtr4d hodval homulcl mp2an hosval 3eqtr4rd rgen hoaddcli hosubcli hoeqi
      mpbi ) EFZAGHZBIJZKJZLZVBABMJZLZNZEOPVEVGNVIEOVBOQZVBALZVBBLZRJZVKVBVDLZS
      JZVHVFVJVMVKVCVLTJZSJZVOVKOQVLOQVMVQNVJVKVLUAOOVBACUBOOVBBDUBUCVJVNVPVKSV
      CUDQZOOBUEZVJVNVPNGUFUGZDVCVBBUHUIUJUKOOAUEZVSVJVHVMNCDVBABULUIWAOOVDUEZV
      JVFVONCVRVSWBVTDVCBUMUNZVBAVDUOUIUPUQEVEVGAVDCWCURABCDUSUTVA $.
      $( [27-Aug-2006] $) $( [24-Aug-2006] $)

$(
    @( Distributive law for Hilbert space operators. @)
    hosdi @p |- ( A e. CC ->
                 ( A .op ( S +op T ) ) = ( ( A .op S ) +op ( A .op T ) ) ) @=
?@.

    @( Distributive law for Hilbert space operators. @)
    hoddii @p |- ( A e. CC ->
                 ( A .op ( S -op T ) ) = ( ( A .op S ) -op ( A .op T ) ) ) @=
?@.
$)
  $}

  $( Subtraction of Hilbert space operators expressed in terms of difference
     from zero. $)
  ho0sub $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
                ( S -op T ) = ( S +op ( 0hop -op T ) ) ) $=
    ( chil wf chod co ch0o chos wceq cif opreq1 eqeq12d opreq2 opreq2d ho0f
    elimf ho0subi dedth2h ) CCADZCCBDZABEFZAGBEFZHFZISAGJZBEFZUDUBHFZIUDTBGJZEF
    ZUDGUGEFZHFZIABGGAUDIUAUEUCUFAUDBEKAUDUBHKLBUGIZUEUHUFUJBUGUDEMUKUBUIUDHBUG
    GEMNLUDUGCCAGOPCCBGOPQR $.
    $( [16-Aug-2006] $) $( [25-Jul-2006] $)

  $( The zero operator subtracted from a Hilbert space operator. $)
  hosubid1 $p |- ( T : ~H --> ~H -> ( T -op 0hop ) = T ) $=
    ( chil wf ch0o chod co chos wceq ho0f ho0sub mpan2 hodidi opreq2i a1i
    hoaddid1 3eqtrd ) BBACZADEFZADDEFZGFZADGFZAQBBDCRTHIADJKTUAHQSDAGDILMNAOP
    $.
    $( [16-Aug-2006] $) $( [25-Jul-2006] $)

  $( Relationship between Hilbert space operator addition and subtraction. $)
  honegsub $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                ( T +op ( -u 1 .op U ) ) = ( T -op U ) ) $=
    ( chil wf c1 cneg chot co chos chod wceq ch0o cif opreq1 eqeq12d opreq2
    opreq2d ho0f elimf honegsubi dedth2h ) CCADZCCBDZAEFZBGHZIHZABJHZKUBALMZUEI
    HZUHBJHZKUHUDUCBLMZGHZIHZUHUKJHZKABLLAUHKUFUIUGUJAUHUEINAUHBJNOBUKKZUIUMUJU
    NUOUEULUHIBUKUDGPQBUKUHJPOUHUKCCALRSCCBLRSTUA $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  ${
    $d x A $.  $d x B $.  $d x T $.  $d x U $.
    $( An operator equals its scalar product with one. $)
    homulid2 $p |- ( T : ~H --> ~H -> ( 1 .op T ) = T ) $=
      ( vx chil wf cv c1 chot co cfv wceq wral wcel wa csm cc ax1cn homval
      mp3an1 ffvelrn ax-hvmulid syl eqtrd r19.21aiva wb homulcl mpan hoeq
      mpancom mpbid ) CCADZBEZFAGHZIZUKAIZJZBCKZULAJZUJUOBCUJUKCLZMZUMFUNNHZUNF
      OLZUJURUMUTJPFUKAQRUSUNCLUTUNJCCUKASUNTUAUBUCCCULDZUJUPUQUDVAUJVBPFAUEUFB
      ULAUGUHUI $.
      $( [16-Aug-2006] $) $( [12-Aug-2006] $)

    $( Associative law for scalar product and composition of operators. $)
    homco1 $p |- ( ( A e. CC /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( ( A .op T ) o. U ) = ( A .op ( T o. U ) ) ) $=
      ( vx cc wcel chil wf w3a cv chot co ccom cfv wceq wral wa csm homval
      ffvelrn syl3an3 3expa exp43 3imp1 wi wfun fvco3 homulcl ffun syl syl3an1
      3expia 3impa imp opreq2d 3adantl1 3eqtr4d fco syl3an2 3impb eqtr4d
      r19.21aiva wb hoeq sylan sylan2 sylanc mpbid ) AEFZGGBHZGGCHZIZDJZABKLZCM
      ZNZVMABCMZKLZNZOZDGPZVOVROZVLVTDGVLVMGFZQZVPAVMVQNZRLZVSWDVMCNZVNNZAWGBNZ
      RLZVPWFVIVJVKWCWHWJOZVIVJVKWCWKVIVJVKWCQZWKVIVJWGGFWKWLAWGBSGGVMCTUAUBUCU
      DVLWCVPWHOZVIVJVKWCWMUEVIVJQZVKWCWMVNUFZVKWCWMWNGGVMVNCUGWNGGVNHZWOABUHZG
      GVNUIUJUKULUMUNVJVKWCWFWJOVIVJVKQZWCQWEWIARVJVKWCWEWIOZBUFVKWCWSVJGGVMBCU
      GGGBUIUKUBUOUPUQVLWCVSWFOZVIVJVKWCWTUEVIWRWCWTVIGGVQHZWCWTWRAVMVQSGGGBCUR
      ZUSULUTUNVAVBGGVOHZGGVRHZWAWBVCVLDVOVRVDVIVJVKXCWPVKXCWNGGGVNCURWQVEUMVIV
      JVKXDVIXAXDWRAVQUHXBVFUTVGVH $.
      $( [20-Aug-2006] $) $( [13-Aug-2006] $)

    $( Scalar product associative law for Hilbert space operators. $)
    homulass $p |- ( ( A e. CC /\ B e. CC /\ T : ~H --> ~H ) ->
                  ( ( A x. B ) .op T ) = ( A .op ( B .op T ) ) ) $=
      ( vx cc wcel chil wf w3a cv cmul co chot cfv wceq wral wa csm
      ax-hvmulass ffvelrn syl3an3 3expa exp43 3imp1 wi homval mulcl syl3an1
      3expia 3impa imp opreq2d 3adantl1 3eqtr4d homulcl syl3an2 3impb eqtr4d
      r19.21aiva wb hoeq sylan sylan2 sylanc mpbid ) AEFZBEFZGGCHZIZDJZABKLZCML
      ZNZVJABCMLZMLZNZOZDGPZVLVOOZVIVQDGVIVJGFZQZVMAVJVNNZRLZVPWAVKVJCNZRLZABWD
      RLZRLZVMWCVFVGVHVTWEWGOZVFVGVHVTWHVFVGVHVTQZWHVFVGWDGFWHWIABWDSGGVJCTUAUB
      UCUDVIVTVMWEOZVFVGVHVTWJUEVFVGQZVHVTWJVKEFZVHVTWJWKVKVJCUFABUGZUHUIUJUKVG
      VHVTWCWGOZVFVGVHVTWNVGVHVTIWBWFARBVJCUFULUBUMUNVIVTVPWCOZVFVGVHVTWOUEVFVG
      VHQZVTWOVFGGVNHZVTWOWPAVJVNUFBCUOZUPUIUQUKURUSGGVLHZGGVOHZVRVSUTVIDVLVOVA
      VFVGVHWSWLVHWSWKVKCUOWMVBUJVFVGVHWTVFWQWTWPAVNUOWRVCUQVDVE $.
      $( [18-Aug-2006] $) $( [12-Aug-2006] $)

    $( Scalar product distributive law for Hilbert space operators. $)
    hoadddi $p |- ( ( A e. CC /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                 ( A .op ( T +op U ) ) = ( ( A .op T ) +op ( A .op U ) ) ) $=
      ( vx cc wcel chil wf w3a cv chos co chot cfv wceq wral wa cva csm homval
      3expa hoaddcl anim2i 3impb sylan hosval opreq2d 3adantl1 ax-hvdistr1
      3simp1 adantr ffvelrn 3ad2antl2 3ad2antl3 syl3anc 3adantl3 3adantl2
      opreq12d eqtr4d 3eqtrd homulcl anim12i 3impdi r19.21aiva wb hoeq sylan2
      syl2an sylanc mpbid ) AEFZGGBHZGGCHZIZDJZABCKLZMLZNZVOABMLZACMLZKLZNZOZDG
      PZVQWAOZVNWCDGVNVOGFZQZVRVOVSNZVOVTNZRLZWBWGVRAVOVPNZSLZAVOBNZVOCNZRLZSLZ
      WJVKGGVPHZQZWFVRWLOZVNVKWQWFWSAVOVPTUAVKVLVMWRVLVMQZWQVKBCUBZUCUDUEVLVMWF
      WLWPOZVKVLVMWFXBVLVMWFIWKWOASVOBCUFUGUAUHWGWPAWMSLZAWNSLZRLZWJVKWMGFZWNGF
      ZWPXEOWGAWMWNUIVNVKWFVKVLVMUJUKVLVKWFXFVMGGVOBULUMVMVKWFXGVLGGVOCULUNUOWG
      WHXCWIXDRVKVLWFWHXCOZVMVKVLWFXHAVOBTUAUPVKVMWFWIXDOZVLVKVMWFXIAVOCTUAUQUR
      USUTGGVSHZGGVTHZQZWFWBWJOZVNXJXKWFXMVOVSVTUFUAVKVLVMXLVKVLQZXJVKVMQZXKABV
      AZACVAZVBVCUEUSVDGGVQHZGGWAHZWDWEVEVNDVQWAVFVKVLVMXRVKWQXRWTAVPVAXAVGUDVK
      VLVMXSXJXKXSXNXOVSVTUBXPXQVHVCVIVJ $.
      $( [19-Aug-2006] $) $( [12-Aug-2006] $)

    $( Scalar product reverse distributive law for Hilbert space operators. $)
    hoadddir $p |- ( ( A e. CC /\ B e. CC /\ T : ~H --> ~H ) ->
                 ( ( A + B ) .op T ) = ( ( A .op T ) +op ( B .op T ) ) ) $=
      ( vx cc wcel chil wf w3a cv caddc co chot cfv chos wceq wral wa cva csm
      ax-hvdistr2 ffvelrn syl3an3 3exp exp4a 3imp1 homval 3expa addcl anim1i
      3impa sylan 3adantl2 3adantl1 opreq12d 3eqtr4d hosval homulcl anim12i
      3impdir eqtr4d r19.21aiva wb hoeq hoaddcl syl2an sylanc mpbid ) AEFZBEFZG
      GCHZIZDJZABKLZCMLZNZVMACMLZBCMLZOLZNZPZDGQZVOVSPZVLWADGVLVMGFZRZVPVMVQNZV
      MVRNZSLZVTWEVNVMCNZTLZAWITLZBWITLZSLZVPWHVIVJVKWDWJWMPZVIVJVKWDWNVIVJVKWD
      RZWNVIVJWIGFWNWOABWIUAGGVMCUBUCUDUEUFVNEFZVKRZWDVPWJPZVLWPVKWDWRVNVMCUGUH
      VIVJVKWQVIVJRZWPVKABUIZUJUKULWEWFWKWGWLSVIVKWDWFWKPZVJVIVKWDXAAVMCUGUHUMV
      JVKWDWGWLPZVIVJVKWDXBBVMCUGUHUNUOUPGGVQHZGGVRHZRZWDVTWHPZVLXCXDWDXFVMVQVR
      UQUHVIVKVJXEVIVKRZXCVJVKRZXDACURZBCURZUSUTULVAVBGGVOHZGGVSHZWBWCVCVLDVOVS
      VDVIVJVKXKWPVKXKWSVNCURWTULUKVIVKVJXLXCXDXLXGXHVQVRVEXIXJVFUTVGVH $.
      $( [2-Sep-2006] $) $( [25-Aug-2006] $)
  $}

  $( Swap first and second factors in a nested operator scalar product. $)
  homul12 $p |- ( ( A e. CC /\ B e. CC /\ T : ~H --> ~H ) ->
                ( A .op ( B .op T ) ) = ( B .op ( A .op T ) ) ) $=
    ( cc wcel chil wf w3a cmul co chot wceq wa mulcom opreq1d 3adant3 homulass
    3com12 3eqtr3d ) ADEZBDEZFFCGZHABIJZCKJZBAIJZCKJZABCKJKJBACKJKJZTUAUDUFLUBT
    UAMUCUECKABNOPABCQUATUBUFUGLBACQRS $.
    $( [19-Aug-2006] $) $( [12-Aug-2006] $)

  $( Double negative of a Hilbert space operator. $)
  honegneg $p |- ( T : ~H --> ~H -> ( -u 1 .op ( -u 1 .op T ) ) = T ) $=
    ( chil wf c1 cneg cmul co chot wceq ax1cn mul2negi mulid1i eqtri opreq1i
    a1i cc wcel negcli homulass mp3an12 homulid2 3eqtr3d ) BBACZDEZUDFGZAHGZDAH
    GZUDUDAHGHGZAUFUGIUCUEDAHUEDDFGDDDJJKDJLMNOUDPQZUIUCUFUHIDJRZUJUDUDASTAUAUB
    $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  $( Relationship between operator subtraction and negative. $)
  hosubneg $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( T -op ( -u 1 .op U ) ) = ( T +op U ) ) $=
    ( chil wf wa c1 cneg chot co chos chod wceq honegsub cc wcel ax1cn negcli
    homulcl mpan sylan2 honegneg opreq2d adantl eqtr3d ) CCADZCCBDZEAFGZUGBHIZH
    IZJIZAUHKIZABJIZUECCUHDZUJUKLUFAUHMUGNOUFUMFPQUGBRSTUFUJULLUEUFUIBAJBUAUBUC
    UD $.
    $( [2-Sep-2006] $) $( [25-Aug-2006] $)

  $( Scalar product distributive law for operator difference. $)
  hosubdi $p |- ( ( A e. CC /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
               ( A .op ( T -op U ) ) = ( ( A .op T ) -op ( A .op U ) ) ) $=
    ( cc wcel chil wf w3a c1 cneg chot co chos chod wceq hoadddi ax1cn negcli
    homulcl mpan syl3an3 homul12 mp3an2 3adant2 opreq2d eqtrd wa honegsub
    3adant1 syl2an 3impdi 3eqtr3d ) ADEZFFBGZFFCGZHZABIJZCKLZMLZKLZABKLZUQACKLZ
    KLZMLZABCNLZKLZVAVBNLZUPUTVAAURKLZMLZVDUMUNFFURGZUTVIOUOABURPUQDEZUOVJIQRZU
    QCSTUAUPVHVCVAMUMUOVHVCOZUNUMVKUOVMVLAUQCUBUCUDUEUFUNUOUTVFOUMUNUOUGUSVEAKB
    CUHUEUIUMUNUOVDVGOZFFVAGFFVBGVNUMUNUGUMUOUGVAVBUHABSACSUJUKUL $.
    $( [19-Aug-2006] $) $( [12-Aug-2006] $)

  $( Distribution of negative over addition. $)
  honegdi $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
          ( -u 1 .op ( T +op U ) ) = ( ( -u 1 .op T ) +op ( -u 1 .op U ) ) ) $=
    ( c1 cneg cc wcel chil wf chos co chot wceq ax1cn negcli hoadddi mp3an1 )
    CDZEFGGAHGGBHQABIJKJQAKJQBKJIJLCMNQABOP $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  $( Distribution of negative over subtraction. $)
  honegsubdi $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                    ( -u 1 .op ( T -op U ) ) = ( ( -u 1 .op T ) +op U ) ) $=
    ( chil wf wa c1 cneg chot co chos chod wceq honegdi cc wcel ax1cn negcli
    homulcl mpan sylan2 honegsub opreq2d honegneg adantl 3eqtr3d ) CCADZCCBDZEZ
    FGZAUIBHIZJIZHIZUIAHIZUIUJHIZJIZUIABKIZHIUMBJIUFCCUJDZULUOLUGAUJMUINOUGUQFP
    QUIBRSTUHUKUPUIHABUAUBUHUNBUMJUGUNBLUFBUCUDUBUE $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  $( Distribution of negative over subtraction. $)
  honegsubdi2 $p |- ( ( T : ~H --> ~H /\ U : ~H --> ~H ) ->
                     ( -u 1 .op ( T -op U ) ) = ( U -op T ) ) $=
    ( chil wf wa c1 cneg chod co chot chos honegsubdi wceq hoaddcom cc wcel
    ax1cn negcli homulcl mpan sylan honegsub ancoms 3eqtrd ) CCADZCCBDZEFGZABHI
    JIUGAJIZBKIZBUHKIZBAHIZABLCCUHDZUFUIUJMUEUHBNUGOPUEULFQRUGASTUAUFUEUJUKMBAU
    BUCUD $.
    $( [2-Sep-2006] $) $( [24-Aug-2006] $)

  $( Law for double subtraction of Hilbert space operators. $)
  hosubsub2 $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( S -op ( T -op U ) ) = ( S +op ( U -op T ) ) ) $=
    ( chil wf w3a c1 cneg chod co chot chos wceq wa honegsub hosubcl sylan2
    3impb honegsubdi2 opreq2d 3adant1 eqtr3d ) DDAEZDDBEZDDCEZFAGHBCIJZKJZLJZAU
    FIJZACBIJZLJZUCUDUEUHUIMZUCDDUFEULUDUENZAUFOBCPQRUDUEUHUKMUCUMUGUJALBCSTUAU
    B $.
    $( [3-Sep-2006] $) $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a mixed addition and subtraction of Hilbert
     space operators. $)
  hosub4 $p |- ( ( ( R : ~H --> ~H /\ S : ~H --> ~H ) /\
      ( T : ~H --> ~H /\ U : ~H --> ~H ) ) -> ( ( R +op S ) -op ( T +op U ) ) =
                ( ( R -op T ) +op ( S -op U ) ) ) $=
    ( chil wf wa chos co c1 cneg chot chod wceq honegdi adantl opreq2d hoadd4
    cc wcel ax1cn negcli homulcl mpan anim12i sylan2 eqtrd honegsub hoaddcl
    syl2an ad2ant2r ad2ant2l opreq12d 3eqtr3d ) EEAFZEEBFZGZEECFZEEDFZGZGZABHIZ
    JKZCDHIZLIZHIZAVCCLIZHIZBVCDLIZHIZHIZVBVDMIZACMIZBDMIZHIVAVFVBVGVIHIZHIZVKV
    AVEVOVBHUTVEVONUQCDOPQUQEEVGFZEEVIFZGVPVKNUTABVGVIRURVQUSVRVCSTZURVQJUAUBZV
    CCUCUDVSUSVRVTVCDUCUDUEUFUGEEVBFEEVDFVFVLNUQUTVBVDUHABUICDUIUJVAVHVMVJVNHUO
    URVHVMNUPUSACUHUKUPUSVJVNNUOURBDUHULUMUN $.
    $( [3-Sep-2006] $) $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a mixed addition and subtraction of Hilbert
     space operators. $)
  hosubadd4 $p |- ( ( ( R : ~H --> ~H /\ S : ~H --> ~H ) /\
      ( T : ~H --> ~H /\ U : ~H --> ~H ) ) -> ( ( R -op S ) -op ( T -op U ) ) =
                ( ( R +op U ) -op ( S +op T ) ) ) $=
    ( chil wf wa chod co chos wceq hosubsub2 3expb hosubcl sylan hosub4 an42s
    eqtr4d ) EEAFZEEBFZGZEECFZEEDFZGZGABHIZCDHIHIZUEDCHIJIZADJIBCJIHIZEEUEFZUDU
    FUGKZUAUIUBUCUJUECDLMABNOSUCTUBUHUGKADBCPQR $.
    $( [6-Sep-2006] $) $( [24-Aug-2006] $)

  $( Associative-type law for addition and subtraction of Hilbert space
     operators. $)
  hoaddsubass $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( ( S +op T ) -op U ) = ( S +op ( T -op U ) ) ) $=
    ( chil wf w3a chos co ch0o chod wceq hoaddass ho0f hosubcl mpan syl3an3 wa
    ho0sub hoaddcl sylan 3impa 3adant1 opreq2d 3eqtr4d ) DDAEZDDBEZDDCEZFZABGHZ
    ICJHZGHZABUJGHZGHZUICJHZABCJHZGHUEUFDDUJEZUKUMKUGABUJLDDIEUGUPMICNOPUEUFUGU
    NUKKZDDUIEUGUQUEUFQUICRABSTUAUHUOULAGUFUGUOULKUEBCRUBUCUD $.
    $( [25-Aug-2006] $)

  $( Law for operator addition and subtraction of Hilbert space operators. $)
  hoaddsub $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( ( S +op T ) -op U ) = ( ( S -op U ) +op T ) ) $=
    ( chil wf w3a chos co chod wceq wa hoaddcom opreq1d 3adant3 hoaddsubass
    3com12 wi ex hosubcl syl5 exp3a com12 3imp 3eqtrd ) DDAEZDDBEZDDCEZFABGHZCI
    HZBAGHZCIHZBACIHZGHZULBGHZUEUFUIUKJUGUEUFKUHUJCIABLMNUFUEUGUKUMJBACOPUEUFUG
    UMUNJZUFUEUGUOQUFUEUGUOUFDDULEZUOUEUGKUFUPUOBULLRACSTUAUBUCUD $.
    $( [9-Sep-2006] $) $( [25-Aug-2006] $)

  $( Law for double subtraction of Hilbert space operators. $)
  hosubsub $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( S -op ( T -op U ) ) = ( ( S -op T ) +op U ) ) $=
    ( chil wf w3a chod co chos hosubsub2 wceq hoaddsubass hoaddsub eqtr3d
    3com23 eqtrd ) DDAEZDDBEZDDCEZFABCGHGHACBGHIHZABGHCIHZABCJQSRTUAKQSRFACIHBG
    HTUAACBLACBMNOP $.
    $( [9-Sep-2006] $) $( [25-Aug-2006] $)

  $( Law for double subtraction of Hilbert space operators. $)
  hosubsub4 $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ U : ~H --> ~H ) ->
                  ( ( S -op T ) -op U ) = ( S -op ( T +op U ) ) ) $=
    ( chil wf w3a c1 cneg chot co chod chos wceq hosubsub cc wcel ax1cn negcli
    homulcl mpan syl3an3 hosubneg 3adant1 opreq2d wa honegsub hosubcl sylan
    3impa 3eqtr3rd ) DDAEZDDBEZDDCEZFZABGHZCIJZKJZKJZABKJZUPLJZABCLJZKJUSCKJZUK
    ULDDUPEZURUTMUMABUPNUOOPUMVCGQRUOCSTUAUNUQVAAKULUMUQVAMUKBCUBUCUDUKULUMUTVB
    MZDDUSEUMVDUKULUEUSCUFABUGUHUIUJ $.
    $( [10-Sep-2006] $) $( [25-Aug-2006] $)

  $( Two times a Hilbert space operator. $)
  ho2times $p |- ( T : ~H --> ~H -> ( 2 .op T ) = ( T +op T ) ) $=
    ( chil wf c2 chot co c1 chos caddc cc wcel wceq ax1cn hoadddir mp3an12
    df-2 opreq1i syl5eq hoadddi mp3an1 anidms hoaddcl homulid2 syl 3eqtr2d ) BB
    ACZDAEFZGAEFZUHHFZGAAHFZEFZUJUFGGIFZAEFZUIUGGJKZUNUFUMUILMMGGANODULAEPQRUFU
    KUILZUNUFUFUOMGAASTUAUFBBUJCZUKUJLUFUPAAUBUAUJUCUDUE $.
    $( [10-Sep-2006] $) $( [26-Aug-2006] $)

  ${
    hoaddsubass.1 $e |- R : ~H --> ~H $.
    hoaddsubass.2 $e |- S : ~H --> ~H $.
    hoaddsubass.3 $e |- T : ~H --> ~H $.
    $( Associativity of sum and difference of Hilbert space operators. $)
    hoaddsubassi $p |- ( ( R +op S ) -op T ) = ( R +op ( S -op T ) ) $=
      ( chil wf chos co chod wceq hoaddsubass mp3an ) GGAHGGBHGGCHABIJCKJABCKJI
      JLDEFABCMN $.
      $( [18-Nov-2004] $) $( [27-Aug-2004] $)

    $( Law for sum and difference of Hilbert space operators. $)
    hoaddsubi $p |- ( ( R +op S ) -op T ) = ( ( R -op T ) +op S ) $=
      ( chos co chod hoaddcomi opreq1i hoaddsubassi hosubcli 3eqtri ) ABGHZCIHB
      AGHZCIHBACIHZGHQBGHOPCIABDEJKBACEDFLBQEACDFMJN $.
      $( [18-Nov-2004] $) $( [27-Aug-2004] $)
  $}

  ${
    hosd1.2 $e |- T : ~H --> ~H $.
    hosd1.3 $e |- U : ~H --> ~H $.
    $( Hilbert space operator sum expressed in terms of difference. $)
    hosd1i $p |- ( T +op U ) = ( T -op ( 0hop -op U ) ) $=
      ( ch0o chod co chos wceq ho0f hosubcli hoaddcli hoaddcomi hoaddsubassi
      hoaddid1i opreq1i hoaddsubi hodseqi eqtri 3eqtri 3eqtr2i hodsi mpbir
      eqcomi ) AEBFGZFGZABHGZUFUGIUEUGHGZAIUHUGUEHGUGEHGZBFGZAUEUGEBJDKZABCDLZM
      UGEBULJDNUJUGBFGABFGZBHGZAUIUGBFUGULOPABBCDDQUNBUMHGAUMBABCDKDMBADCRSTUAA
      UEUGCUKULUBUCUD $.
      $( [3-Aug-2007] $) $( [27-Aug-2004] $)

    $( Hilbert space operator sum expressed in terms of difference. $)
    hosd2i $p |- ( T +op U ) = ( T -op ( ( U -op U ) -op U ) ) $=
      ( chos co ch0o chod hosd1i hodidi opreq1i opreq2i eqtr4i ) ABEFAGBHFZHFAB
      BHFZBHFZHFABCDIPNAHOGBHBDJKLM $.
      $( [18-Nov-2004] $) $( [27-Aug-2004] $)

    $( Hilbert space operator cancellation law. $)
    hopncani $p |- ( ( T +op U ) -op U ) = T $=
      ( chos co chod ch0o hoaddsubassi hodidi opreq2i hoaddid1i 3eqtri ) ABEFBG
      FABBGFZEFAHEFAABBCDDINHAEBDJKACLM $.
      $( [25-Mar-2006] $) $( [10-Mar-2006] $)

    $( Hilbert space operator cancellation law. $)
    honpcani $p |- ( ( T -op U ) +op U ) = T $=
      ( chos co chod hoaddsubi hopncani eqtr3i ) ABEFBGFABGFBEFAABBCDDHABCDIJ
      $.
      $( [28-Mar-2006] $) $( [11-Mar-2006] $)

    $( If the difference between two operators is zero, they are equal. $)
    hosubeq0i $p |- ( ( T -op U ) = 0hop <-> T = U ) $=
      ( chod co ch0o wceq c1 cneg chot chos honegsubi eqeq1i opreq1 sylbir cc
      wcel chil wf ax1cn negcli homulcl mp2an hoadd23i hoaddassi hodidi eqtri
      opreq2i hoaddid1i 3eqtri ho0f hoaddcomi 3eqtr3g syl6eq impbii ) ABEFZGHZA
      BHZURAIJZBKFZLFZBLFZGBLFZABURVBGHVCVDHVBUQGABCDMNVBGBLOPVCABLFVALFABVALFZ
      LFZAAVABCUTQRSSBTSSVATIUAUBDUTBUCUDZDUEABVACDVGUFVFAGLFAVEGALVEBBEFZGBBDD
      MBDUGZUHUIACUJUHUKVDBGLFBGBULDUMBDUJUHUNUSUQVHGABBEOVIUOUP $.
      $( [31-Jul-2007] $) $( [27-Jul-2006] $)
  $}

  ${
    honpncan.1 $e |- R : ~H --> ~H $.
    honpncan.2 $e |- S : ~H --> ~H $.
    honpncan.3 $e |- T : ~H --> ~H $.
    $( Hilbert space operator cancellation law. $)
    honpncani $p |- ( ( R -op S ) +op ( S -op T ) ) = ( R -op T ) $=
      ( chod co chos hosubcli hoaddsubassi honpcani opreq1i eqtr3i ) ABGHZBIHZC
      GHOBCGHIHACGHOBCABDEJEFKPACGABDELMN $.
      $( [30-Apr-2006] $) $( [11-Mar-2006] $)
  $}

  ${
    $d x y T $.
    ho0.1 $e |- T : ~H --> ~H $.
    $( A condition implying that a Hilbert space operator is identically zero.
       Lemma 3.2(S8) of [Beran] p. 95. $)
    ho01i $p |- ( A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = 0 <->
               T = 0hop ) $=
      ( chil c0v csn cxp wceq cv cfv wral ch0o csp co cc0 wfn wb wf ffn ax-mp
      ax-hv0cl elisseti fconst eqfnfv2 mp2an c0h df0op2 df-ch0 xpeq2i eqtri
      eqeq2i wcel ffvelrni hial0 syl fvconst2 eqeq2d bitr4d ralbiia 3bitr4ri )
      CEFGZHZIZAJZCKZVEVCKZIZAELZCMIVFBJNOPIBELZAELCEQZVCEQZVDVIREECSVKDEECTUAE
      VBVCSVLEFFEUBUCZUDEVBVCTUAAECVCUEUFMVCCMEUGHVCUHUGVBEUIUJUKULVJVHAEVEEUMZ
      VJVFFIZVHVNVFEUMVJVOREEVECDUNBVFUOUPVNVGFVFEFVEVMUQURUSUTVA $.
      $( [31-Jul-2007] $) $( [28-Jan-2006] $)

    $( A condition implying that a Hilbert space operator is identically zero.
       Lemma 3.2(S10) of [Beran] p. 95. $)
    ho02i $p |- ( A. x e. ~H A. y e. ~H ( x .ih ( T ` y ) ) = 0 <->
               T = 0hop ) $=
      ( cv cfv csp co cc0 wceq chil wral ch0o ralcom wcel wb ffvelrni c0v
      hial02 hial0 bitr4d syl ralbiia ho01i 3bitri ) AEZBEZCFZGHIJZBKLAKLUIAKLZ
      BKLUHUFGHIJAKLZBKLCMJUIABKKNUJUKBKUGKOUHKOZUJUKPKKUGCDQULUJUHRJUKAUHSAUHT
      UAUBUCBACDUDUE $.
      $( [30-Apr-2008] $) $( [28-Jan-2006] $)
  $}

  ${
    $d x y z S $.  $d u v x y z T $.
    $( A condition implying that two Hilbert space operators are equal.  Lemma
       3.2(S9) of [Beran] p. 95. $)
    hoeq1 $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) -> ( A. x e. ~H
          A. y e. ~H ( ( S ` x ) .ih y ) = ( ( T ` x ) .ih y ) <-> S = T ) ) $=
      ( chil wf wa cv cfv csp co wceq wral wcel wb hial2eq ffvelrn syl2an
      anandirs ralbidva wfn eqfnfv2 ffn bitr4d ) EECFZEEDFZGZAHZCIZBHZJKUHDIZUJ
      JKLBEMZAEMUIUKLZAEMZCDLZUGULUMAEUEUFUHENZULUMOZUIENUKENUQUEUPGUFUPGBUIUKP
      EEUHCQEEUHDQRSTCEUADEUAUOUNOUEUFAECDUBEECUCEEDUCRUD $.
      $( [21-Feb-2006] $) $( [15-Feb-2006] $)

    $( A condition implying that two Hilbert space operators are equal.  Lemma
       3.2(S11) of [Beran] p. 95. $)
    hoeq2 $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) -> ( A. x e. ~H
          A. y e. ~H ( x .ih ( S ` y ) ) = ( x .ih ( T ` y ) ) <-> S = T ) ) $=
      ( chil wf wa cv cfv csp co wceq wral wb ralcom a1i wcel hial2eq2 hial2eq
      bitr4d ffvelrn syl2an anandirs ralbidva hoeq1 3bitrd ) EECFZEEDFZGZAHZBHZ
      CIZJKUJUKDIZJKLZBEMAEMZUNAEMZBEMZULUJJKUMUJJKLAEMZBEMCDLUOUQNUIUNABEEOPUI
      UPURBEUGUHUKEQZUPURNZULEQZUMEQZUTUGUSGUHUSGVAVBGUPULUMLURAULUMRAULUMSTEEU
      KCUAEEUKDUAUBUCUDBACDUEUF $.
      $( [26-May-2006] $) $( [15-Feb-2006] $)

    $( Every Hilbert space operator has at most one adjoint. $)
    adjmo $p |- E* u ( u : ~H --> ~H /\
           A. x e. ~H A. y e. ~H ( x .ih ( T ` y ) ) = ( ( u ` x ) .ih y ) ) $=
      ( vv chil cv wf cfv csp co wceq wral wa wmo wi wal hoeq1 biimpa r19.26-2
      eqtr2 ralimi sylbir sylan2 an4s gen2 feq1 fveq1 opreq1d eqeq2d 2ralbidv
      anbi12d mo4 mpbir ) FFCGZHZAGZBGZDIJKZUQUOIZURJKZLZBFMAFMZNZCOVDFFEGZHZUS
      UQVEIZURJKZLZBFMAFMZNZNUOVELZPZEQCQVMCEUPVFVCVJVLUPVFNZVAVHLZBFMZAFMZVLVC
      VJNZVNVQVLABUOVERSVRVBVINZBFMZAFMVQVBVIABFFTVTVPAFVSVOBFUSVAVHUAUBUBUCUDU
      EUFVDVKCEVLUPVFVCVJFFUOVEUGVLVBVIABFFVLVAVHUSVLUTVGURJUQUOVEUHUIUJUKULUMU
      N $.
      $( [21-Feb-2006] $) $( [18-Feb-2006] $)

    $( Symmetry property of an adjoint. $)
    adjsym $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) ->
          ( A. x e. ~H A. y e. ~H ( x .ih ( S ` y ) ) = ( ( T ` x ) .ih y ) <->
         A. x e. ~H A. y e. ~H ( x .ih ( T ` y ) ) = ( ( S ` x ) .ih y ) ) ) $=
      ( vz chil wf wa cv cfv csp co wceq wral wcel wb ccj ax-his1 ffvelrn
      sylan adantrl sylan2 adantll eqeq12d ancoms cc cj11 hicl sylanc bitr2d
      an4s anassrs eqcom syl6bb ralbidva ralcom fveq2 opreq2d opreq2 ralbidv
      cbvralv bitr4i opreq1 opreq1d ralbii 3bitri syl6rbbr ) FFCGZFFDGZHZAIZBIZ
      DJZKLZVKCJZVLKLZMZBFNZAFNVLVOKLZVMVKKLZMZBFNZAFNZVKVLCJZKLZVKDJZVLKLZMZBF
      NAFNZVJVRWBAFVJVKFOZHZVQWABFWKVLFOZHVQVTVSMZWAVJWJWLVQWMPZVHWJVIWLWNVHWJH
      ZVIWLHZHZWMVNQJZVPQJZMZVQWPWOWMWTPWPWOHVTWRVSWSWPWJVTWRMZVHVMFOZWJXAWPVMV
      KRFFVLDSZTUAWLWOVSWSMZVIWLVOFOZXDWOVLVORFFVKCSZUBUCUDUEVNUFOZVPUFOZWTVQPW
      QVNVPUGWJWPXGVHWJXBXGWPVKVMUHXCUBUCWOWLXHVIXEWLXHWOVOVLUHXFTUAUIUJUKULVTV
      SUMUNUOUOWIVKEIZCJZKLZWFXIKLZMZAFNZEFNZVLXJKLZVMXIKLZMZBFNZEFNWCWIWHAFNZB
      FNXOWHABFFUPXNXTEBFXIVLMZXMWHAFYAXKWEXLWGYAXJWDVKKXIVLCUQURXIVLWFKUSUDUTV
      AVBXNXSEFXMXRABFVKVLMZXKXPXLXQVKVLXJKVCYBWFVMXIKVKVLDUQVDUDVAVEXSWBEAFXIV
      KMZXRWABFYCXPVSXQVTYCXJVOVLKXIVKCUQURXIVKVMKUSUDUTVAVFVG $.
      $( [25-Feb-2006] $) $( [18-Feb-2006] $)
  $}

  ${
    eigre.1 $e |- A e. ~H $.
    eigre.2 $e |- B e. CC $.
    $( A necessary and sufficient condition (that holds when ` T ` is a
       Hermitian operator) for an eigenvalue ` B ` to be real.  Generalization
       of Equation 1.30 of [Hughes] p. 49. $)
    eigrei $p |- ( ( ( T ` A ) = ( B .h A ) /\ A =/= 0h ) ->
                 ( ( A .ih ( T ` A ) ) = ( ( T ` A ) .ih A ) <-> B e. RR ) ) $=
      ( cfv csm co wceq c0v wne wa csp ccj cr wcel cmul opreq2 cc chil his5
      mp3an syl6eq opreq1 ax-his3 eqeq12d cc0 clt wbr wb ax-his4 mpan hiidrcl
      ax-mp gt0ne0i hicli cjcli mulcan2 mp3an1 3syl sylan9bb cjrebi syl6bbr )
      ACFZBAGHZIZAJKZLAVDMHZVDAMHZIZBNFZBIZBOPVFVJVKAAMHZQHZBVMQHZIZVGVLVFVHVNV
      IVOVFVHAVEMHZVNVDVEAMRBSPZATPZVSVQVNIEDDBAAUAUBUCVFVIVEAMHZVOVDVEAMUDVRVS
      VSVTVOIEDDBAAUEUBUCUFVGUGVMUHUIZVMUGKZVPVLUJZVSVGWADAUKULVMVSVMOPDAUMUNUO
      VMSPZWBWCAADDUPVRWDWBLZWCEVKSPVRWEWCBEUQVKBVMURUSULULUTVABEVBVC $.
      $( [18-May-2006] $) $( [21-Jan-2005] $)
  $}

  $( A necessary and sufficient condition (that holds when ` T ` is a Hermitian
     operator) for an eigenvalue ` B ` to be real.  Generalization of Equation
     1.30 of [Hughes] p. 49. $)
  eigre $p |- ( ( ( A e. ~H /\ B e. CC ) /\ ( ( T ` A ) = ( B .h A ) /\ A =/=
       0h ) ) -> ( ( A .ih ( T ` A ) ) = ( ( T ` A ) .ih A ) <-> B e. RR ) ) $=
    ( chil wcel cc wa cfv csm co wceq c0v wne csp cr wb wi cif cc0 fveq2
    opreq2 eqeq12d neeq1 anbi12d id opreq12d bibi1d imbi12d opreq1 eqeq2d
    anbi1d eleq1 bibi2d ax-hv0cl elimel 0cn eigrei dedth2h imp ) ADEZBFEZGACHZB
    AIJZKZALMZGZAVBNJZVBANJZKZBOEZPZUTVAVFVKQUTALRZCHZBVLIJZKZVLLMZGZVLVMNJZVMV
    LNJZKZVJPZQVMVABSRZVLIJZKZVPGZVTWBOEZPZQABLSAVLKZVFVQVKWAWHVDVOVEVPWHVBVMVC
    VNAVLCTZAVLBIUAUBAVLLUCUDWHVIVTVJWHVGVRVHVSWHAVLVBVMNWHUEZWIUFWHVBVMAVLNWIW
    JUFUBUGUHBWBKZVQWEWAWGWKVOWDVPWKVNWCVMBWBVLIUIUJUKWKVJWFVTBWBOULUMUHVLWBCAL
    DUNUOBSFUPUOUQURUS $.
    $( [26-May-2006] $) $( [19-Mar-2006] $)

  ${
    eigpos.1 $e |- A e. ~H $.
    eigpos.2 $e |- B e. CC $.
    $( A sufficient condition (first conjunct pair, that holds when ` T ` is a
       positive operator) for an eigenvalue ` B ` (second conjunct pair) to be
       nonnegative.  Remark (ii) in [Hughes] p. 137. $)
    eigposi $p |- ( ( ( ( A .ih ( T ` A ) ) e. RR /\ 0 <_ ( A .ih ( T ` A ) ) )
      /\ ( ( T ` A ) = ( B .h A ) /\ A =/= 0h ) ) -> ( B e. RR /\ 0 <_ B ) ) $=
      ( cfv csp co cr wcel cc0 cle wbr wa csm wceq c0v wne wb chil hvmulcli
      hire mp2an a1i opreq2 eleq1d opreq1 eqeq12d 3bitr4d adantr eigrei bitrd
      biimpac adantlr clt cmul hiidrcl ax-mp prodge02 mpanl2 ax-his4 mpan
      ad2antll simplr ad2antrl ccj cjrebi sylib opreq1d cc his5 mp3an syl5eq
      eqtrd breqtrd jca sylanc ) AACFZGHZIJZKVSLMZNZVRBAOHZPZAQRZNZNZBIJZKBLMZV
      TWFWHWAWFVTWHWFVTVSVRAGHZPZWHWDVTWKSWEWDAWCGHZIJZWLWCAGHZPZVTWKWMWOSZWDAT
      JZWCTJWPDBAEDUAAWCUBUCUDWDVSWLIVRWCAGUEZUFWDVSWLWJWNWRVRWCAGUGUHUIUJABCDE
      UKULUMUNZWHKAAGHZUOMZKBWTUPHZLMZNZWIWGWHWTIJZXDWIWQXEDAUQURBWTUSUTWSWGXAX
      CWEXAWBWDWQWEXADAVAVBVCWGKVSXBLVTWAWFVDWGVSWLXBWDVSWLPWBWEWRVEWGBVFFZWTUP
      HZXBWLWGXFBWTUPWGWHXFBPWSBEVGVHVIBVJJWQWQWLXGPEDDBAAVKVLVMVNVOVPVQVP $.
      $( [13-Oct-2006] $) $( [2-Jul-2005] $)
  $}

  ${
    eigorthi.1 $e |- A e. ~H $.
    eigorthi.2 $e |- B e. ~H $.
    eigorthi.3 $e |- C e. CC $.
    eigorthi.4 $e |- D e. CC $.
    $( A necessary and sufficient condition (that holds when ` T ` is a
       Hermitian operator) for two eigenvectors ` A ` and ` B ` to be
       orthogonal.  Generalization of Equation 1.31 of [Hughes] p. 49. $)
    eigorthi $p |- ( ( ( ( T ` A ) = ( C .h A ) /\ ( T ` B ) = ( D .h B ) )
                     /\ C =/= ( * ` D ) ) ->
         ( ( A .ih ( T ` B ) ) = ( ( T ` A ) .ih B ) <-> ( A .ih B ) = 0 ) ) $=
      ( cfv csm co wceq wa csp ccj cmul wne cc0 opreq2 cc wcel chil his5 mp3an
      syl6eq opreq1 ax-his3 eqeqan12rd wb hicli cjcli mulcan2 mp3an12 mpan
      eqcom syl6bb biimpcd necon1d com12 mul01i eqtr4i eqtr4d impbid1 sylan9bb
      ) AEJZCAKLZMZBEJZDBKLZMZNAVIOLZVFBOLZMDPJZABOLZQLZCVOQLZMZCVNRZVOSMZVKVHV
      LVPVMVQVKVLAVJOLZVPVIVJAOTDUAUBAUCUBZBUCUBZWAVPMIFGDABUDUEUFVHVMVGBOLZVQV
      FVGBOUGCUAUBZWBWCWDVQMHFGCABUHUEUFUIVSVRVTVRVSVTVRVOSCVNVOSRZVRCVNMZWFVRV
      NCMZWGVOUAUBZWFVRWHUJZABFGUKVNUAUBWEWIWFNWJDIULZHVNCVOUMUNUOVNCUPUQURUSUT
      VTVPVNSQLZVQVOSVNQTVTVQCSQLZWLVOSCQTWMSWLCHVAVNWKVAVBUFVCVDVE $.
      $( [13-Jan-2009] $) $( [23-Jan-2005] $)
  $}

  $( A necessary and sufficient condition (that holds when ` T ` is a Hermitian
     operator) for two eigenvectors ` A ` and ` B ` to be orthogonal.
     Generalization of Equation 1.31 of [Hughes] p. 49. $)
  eigorth $p |- ( ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. CC /\ D e. CC ) ) /\
                 ( ( ( T ` A ) = ( C .h A ) /\ ( T ` B ) = ( D .h B ) )
                   /\ C =/= ( * ` D ) ) ) ->
         ( ( A .ih ( T ` B ) ) = ( ( T ` A ) .ih B ) <-> ( A .ih B ) = 0 ) ) $=
    ( chil wcel wa cc cfv csm co wceq ccj wne csp cc0 wb wi c0v cif fveq2
    opreq2 eqeq12d anbi1d opreq1 opreq1d eqeq1d bibi12d imbi12d anbi2d opreq2d
    eqeq2d neeq1 anbi12d imbi1d neeq2d ax-hv0cl elimel 0cn eigorthi dedth4h
    imp ) AFGZBFGZHCIGZDIGZHHAEJZCAKLZMZBEJZDBKLZMZHZCDNJZOZHZAVKPLZVHBPLZMZABP
    LZQMZRZVDVEVFVGVQWCSVDATUAZEJZCWDKLZMZVMHZVPHZWDVKPLZWEBPLZMZWDBPLZQMZRZSWG
    VEBTUAZEJZDWPKLZMZHZVPHZWDWQPLZWEWPPLZMZWDWPPLZQMZRZSWEVFCQUAZWDKLZMZWSHZXH
    VOOZHZXGSXJWQVGDQUAZWPKLZMZHZXHXNNJZOZHZXGSABCDTTQQAWDMZVQWIWCWOYAVNWHVPYAV
    JWGVMYAVHWEVIWFAWDEUBZAWDCKUCUDUEUEYAVTWLWBWNYAVRWJVSWKAWDVKPUFYAVHWEBPYBUG
    UDYAWAWMQAWDBPUFUHUIUJBWPMZWIXAWOXGYCWHWTVPYCVMWSWGYCVKWQVLWRBWPEUBZBWPDKUC
    UDUKUEYCWLXDWNXFYCWJXBWKXCYCVKWQWDPYDULBWPWEPUCUDYCWMXEQBWPWDPUCUHUIUJCXHMZ
    XAXMXGYEWTXKVPXLYEWGXJWSYEWFXIWECXHWDKUFUMUECXHVOUNUOUPDXNMZXMXTXGYFXKXQXLX
    SYFWSXPXJYFWRXOWQDXNWPKUFUMUKYFVOXRXHDXNNUBUQUOUPWDWPXHXNEATFURUSBTFURUSCQI
    UTUSDQIUTUSVAVBVC $.
    $( [15-Jan-2009] $) $( [23-Mar-2006] $)

