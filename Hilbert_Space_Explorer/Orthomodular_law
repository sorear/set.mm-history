$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Orthomodular law
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    omlsilem.1 $e |- G e. SH $.
    omlsilem.2 $e |- H e. SH $.
    omlsilem.3 $e |- G (_ H $.
    omlsilem.4 $e |- ( H i^i ( _|_ ` G ) ) = 0H $.
    omlsilem.5 $e |- A e. H $.
    omlsilem.6 $e |- B e. G $.
    omlsilem.7 $e |- C e. ( _|_ ` G ) $.
    $( Lemma for orthomodular law in the Hilbert lattice. $)
    omlsilem $p |- ( A = ( B +h C ) -> A e. G ) $=
      ( cva co wceq wcel c0v cmv sheli cort cfv chil csh wss shocss ax-mp
      sselii hvsubadd eqcom bitr wa wi shsubclt mp2an eleq1 mpbii sylbir cin
      c0h eleq2i elin elch0 3bitr3 biimp mpan2 syl opreq2d ax-hvaddid syl6eq
      syl6eqel mpbird ) ABCMNZOZADPVLDPVMVLBDVMVLBQMNZBVMCQBMVMCEPZCQOZVMABRNZC
      OZVOVRVLAOVMABCAEGJSBDFKSZDTUAZUBCDUCPVTUBUDFDUEUFLUGUHVLAUIUJVRVQEPZVOAE
      PZBEPZWAJDEBHKUGEUCPWBWCUKWAULGABEUMUFUNVQCEUOUPUQVOCVTPZVPLVOWDUKZVPCEVT
      URZPCUSPWEVPWFUSCIUTCEVTVACVBVCVDVEVFVGBUBPVNBOVSBVHUFVIKVJAVLDUOVK $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    omlsi.1 $e |- A e. CH $.
    omlsi.2 $e |- B e. SH $.
    omlsi.3 $e |- A (_ B $.
    omlsi.4 $e |- ( B i^i ( _|_ ` A ) ) = 0H $.
    $( Subspace inference form of orthomodular law in the Hilbert lattice. $)
    omlsi $p |- A = B $=
      ( vx vy vz cv wcel cva co wceq cort cfv wrex chil shel cch pjtht mpan
      syl wa wi c0v cif eqeq1 eleq1 imbi12d opreq1 eqeq2d imbi1d opreq2 chshi
      csh sh0 ax-mp elimel ch0 shocsh omlsilem dedth3h 3exp imp r19.23adv
      r19.23adva mpd ssriv eqssi ) ABEGBAGJZBKZVKHJZIJZLMZNZIAOPZQZHAQZVKAKZVLV
      KRKZVSVKBDSATKZWAVSCHIVKAUAUBUCVLVRVTHAVLVMAKZUDVPVTIVQVLWCVNVQKZVPVTUEZU
      EVLWCWDWEVLWCWDWEVLVKUFUGZVONZWFAKZUEVKVMVNUFUFUFWFWCVMUFUGZVNLMZNZWHUEWF
      WIWDVNUFUGZLMZNZWHUEVKWFNVPWGVTWHVKWFVOUHVKWFAUIUJVMWINZWGWKWHWOVOWJWFVMW
      IVNLUKULUMVNWLNZWKWNWHWPWJWMWFVNWLWILUNULUMWFWIWLABACUOZDEFVKUFBBUPKUFBKD
      BUQURUSVMUFAWBUFAKCAUTURUSVNUFVQVQUPKZUFVQKAUPKWRWQAVAURVQUQURUSVBVCVDVEV
      FVGVHVIVJ $.
      $( [14-Oct-1999] $)
  $}

  ${
    omls.1 $e |- A e. CH $.
    omls.2 $e |- B e. SH $.
    $( Subspace form of orthomodular law in the Hilbert lattice.  Compare
       the orthomodular law in Theorem 2(ii) of [Kalmbach] p. 22. $)
    omls $p |- ( ( A (_ B /\ ( B i^i ( _|_ ` A ) ) = 0H ) -> A = B ) $=
      ( wss cort cfv cin c0h wceq wa cif eqeq1 eqeq2 cch h0elch keepel csh
      h0elsh sseq1 fveq2 ineq2d eqeq1d anbi12d sseq2 ineq1 ssid wcel ocin
      ax-mp pm3.2i elimhyp2v pm3.26i pm3.27i omlsi dedth2v ) ABEZBAFGZHZIJZKZAB
      JVAAILZBJVBVABILZJABIIAVBBMBVCVBNVBVCVAAIOCPQVABIRDSQVBVCEZVCVBFGZHZIJZVA
      VBBEZBVEHZIJZKVDVGKIIEZIIFGZHZIJZKABIIVBIEZIVEHZIJZKAVBJZUQVHUTVJAVBBTVRU
      SVIIVRURVEBAVBFUAUBUCUDBVCJZVHVDVJVGBVCVBUEVSVIVFIBVCVEUFUCUDIVBJZVKVOVNV
      QIVBITVTVMVPIVTVLVEIIVBFUAUBUCUDIVCJZVOVDVQVGIVCVBUEWAVPVFIIVCVEUFUCUDVKV
      NIUGIRUHVNSIUIUJUKULZUMVDVGWBUNUOUP $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d x y z A $.
    ococ.1 $e |- A e. CH $.
    $( Complement of complement of a closed subspace of Hilbert space.
       Theorem 3.7(ii) of [Beran] p. 102. $)
    ococ $p |- ( _|_ ` ( _|_ ` A ) ) = A $=
      ( cort cfv csh wcel chshi shocsh ax-mp wss shococss cin c0h incom wceq
      ocin eqtr omlsi eqcomi ) AACDZCDZAUABTEFZUAEFAEFZUBABGZAHIZTHIUCAUAJUDAKI
      UATLTUALZMUATNUBUFMOUETPIQRS $.
      $( [11-Oct-1999] $)
  $}

  ${
    $d x y z A $.
    $( Complement of complement of a closed subspace of Hilbert space.
       Theorem 3.7(ii) of [Beran] p. 102. $)
    ococt $p |- ( A e. CH -> ( _|_ ` ( _|_ ` A ) ) = A ) $=
      ( cch wcel cort cfv wceq chil cif fveq2 fveq2d id eqeq12d helch elimel
      ococ dedth ) ABCZADEZDEZAFQAGHZDEZDEZTFAGATFZSUBATUCRUADATDIJUCKLTAGBMNOP
      $.
      $( [11-Oct-1999] $)
  $}

  $( Alternate definition of the Hilbert lattice. $)
  dfch2 $p |- CH = { x | ( x (_ H~ /\ ( _|_ ` ( _|_ ` x ) ) = x ) } $=
    ( cv chil wss cort cfv wceq wa cch wcel chss ococt jca eleq1 occlt 3syl
    syl5bi impcom impbi abbi2i ) ABZCDZUAEFZEFZUAGZHZAIUAIJZUFUGUBUEUAKUALMUEUB
    UGUEUDIJZUGUBUDUAINUBUCIJUCCDUHUAOUCKUCOPQRST $.
    $( [8-Aug-2000] $)

