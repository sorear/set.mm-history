$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Orthomodular law
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    omlsilem.1 $e |- G e. SH $.
    omlsilem.2 $e |- H e. SH $.
    omlsilem.3 $e |- G C_ H $.
    omlsilem.4 $e |- ( H i^i ( _|_ ` G ) ) = 0H $.
    omlsilem.5 $e |- A e. H $.
    omlsilem.6 $e |- B e. G $.
    omlsilem.7 $e |- C e. ( _|_ ` G ) $.
    $( Lemma for orthomodular law in the Hilbert lattice. $)
    omlsilem $p |- ( A = ( B +h C ) -> A e. G ) $=
      ( cva co wceq wcel c0v cmv shelii cort cfv chil csh wss shocss ax-mp
      sselii hvsubaddi eqcom bitri shsubcl mp3an eleq1 mpbii sylbir wa cin c0h
      eleq2i elin elch0 3bitr3i biimpi mpan2 syl opreq2d ax-hvaddid syl6eq
      syl6eqel mpbird ) ABCMNZOZADPVKDPVLVKBDVLVKBQMNZBVLCQBMVLCEPZCQOZVLABRNZC
      OZVNVQVKAOVLABCAEGJSBDFKSZDTUAZUBCDUCPVSUBUDFDUEUFLUGUHVKAUIUJVQVPEPZVNEU
      CPAEPBEPVTGJDEBHKUGABEUKULVPCEUMUNUOVNCVSPZVOLVNWAUPZVOCEVSUQZPCURPWBVOWC
      URCIUSCEVSUTCVAVBVCVDVEVFBUBPVMBOVRBVGUFVHKVIAVKDUMVJ $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    omlsi.1 $e |- A e. CH $.
    omlsi.2 $e |- B e. SH $.
    omlsi.3 $e |- A C_ B $.
    omlsi.4 $e |- ( B i^i ( _|_ ` A ) ) = 0H $.
    $( Subspace inference form of orthomodular law in the Hilbert lattice. $)
    omlsii $p |- A = B $=
      ( vx vy vz cv wcel cva co wceq cort cfv wrex chil sheli cch pjth mpan
      syl wa wi c0v cif eqeq1 eleq1 imbi12d opreq1 eqeq2d imbi1d opreq2 chshii
      csh sh0 ax-mp elimel ch0 shocsh omlsilem dedth3h 3expia r19.23adv
      r19.23adva mpd ssriv eqssi ) ABEGBAGJZBKZVJHJZIJZLMZNZIAOPZQZHAQZVJAKZVKV
      JRKZVRVJBDSATKZVTVRCHIVJAUAUBUCVKVQVSHAVKVLAKZUDVOVSIVPVKWBVMVPKZVOVSUEZV
      KWBWCWDVKVJUFUGZVNNZWEAKZUEWEWBVLUFUGZVMLMZNZWGUEWEWHWCVMUFUGZLMZNZWGUEVJ
      VLVMUFUFUFVJWENVOWFVSWGVJWEVNUHVJWEAUIUJVLWHNZWFWJWGWNVNWIWEVLWHVMLUKULUM
      VMWKNZWJWMWGWOWIWLWEVMWKWHLUNULUMWEWHWKABACUOZDEFVJUFBBUPKUFBKDBUQURUSVLU
      FAWAUFAKCAUTURUSVMUFVPVPUPKZUFVPKAUPKWQWPAVAURVPUQURUSVBVCVDVEVFVGVHVI $.
      $( [14-Oct-1999] $)
  $}

  ${
    omls.1 $e |- A e. CH $.
    omls.2 $e |- B e. SH $.
    $( Subspace form of orthomodular law in the Hilbert lattice.  Compare the
       orthomodular law in Theorem 2(ii) of [Kalmbach] p. 22. $)
    omlsi $p |- ( ( A C_ B /\ ( B i^i ( _|_ ` A ) ) = 0H ) -> A = B ) $=
      ( wss cort cfv cin c0h wceq wa cif eqeq1 eqeq2 cch h0elch keepel csh
      h0elsh sseq1 fveq2 ineq2d eqeq1d anbi12d sseq2 ineq1 ssid wcel ocin
      ax-mp pm3.2i elimhyp2v simpli simpri omlsii dedth2v ) ABEZBAFGZHZIJZKZABJ
      VAAILZBJVBVABILZJABIIAVBBMBVCVBNVBVCVAAIOCPQVABIRDSQVBVCEZVCVBFGZHZIJZVAV
      BBEZBVEHZIJZKVDVGKIIEZIIFGZHZIJZKVBIEZIVEHZIJZKABIIAVBJZUQVHUTVJAVBBTVRUS
      VIIVRURVEBAVBFUAUBUCUDBVCJZVHVDVJVGBVCVBUEVSVIVFIBVCVEUFUCUDIVBJZVKVOVNVQ
      IVBITVTVMVPIVTVLVEIIVBFUAUBUCUDIVCJZVOVDVQVGIVCVBUEWAVPVFIIVCVEUFUCUDVKVN
      IUGIRUHVNSIUIUJUKULZUMVDVGWBUNUOUP $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d x y z A $.
    ococ.1 $e |- A e. CH $.
    $( Complement of complement of a closed subspace of Hilbert space.  Theorem
       3.7(ii) of [Beran] p. 102. $)
    ococi $p |- ( _|_ ` ( _|_ ` A ) ) = A $=
      ( cort cfv csh wcel chshii shocsh ax-mp wss shococss cin c0h incom wceq
      ocin eqtri omlsii eqcomi ) AACDZCDZAUABTEFZUAEFAEFZUBABGZAHIZTHIUCAUAJUDA
      KIUATLTUALZMUATNUBUFMOUETPIQRS $.
      $( [11-Oct-1999] $)
  $}

  ${
    $d x y z A $.
    $( Complement of complement of a closed subspace of Hilbert space.  Theorem
       3.7(ii) of [Beran] p. 102. $)
    ococ $p |- ( A e. CH -> ( _|_ ` ( _|_ ` A ) ) = A ) $=
      ( cch wcel cort cfv wceq chil cif fveq2 fveq2d id eqeq12d helch elimel
      ococi dedth ) ABCZADEZDEZAFQAGHZDEZDEZTFAGATFZSUBATUCRUADATDIJUCKLTAGBMNO
      P $.
      $( [11-Oct-1999] $)
  $}

  $( Alternate definition of the Hilbert lattice.  (Revised by Mario Carneiro,
     23-Dec-2013.) $)
  dfch2 $p |- CH = { x e. ~P ~H | ( _|_ ` ( _|_ ` x ) ) = x } $=
    ( cch cv chil cpw wcel cort cfv wceq wa cab crab wss chss ococ jca occl
    3syl eleq1 syl5ib impcom impbii visset elpw anbi1i bitr4i abbi2i df-rab
    eqtr4i ) BACZDEZFZUJGHZGHZUJIZJZAKUOAUKLUPABUJBFZUJDMZUOJZUPUQUSUQURUOUJNUJ
    OPUOURUQURUNBFZUOUQURUMBFUMDMUTUJQUMNUMQRUNUJBSTUAUBULURUOUJDAUCUDUEUFUGUOA
    UKUHUI $.
    $( [23-Dec-2013] $) $( [8-Aug-2000] $)

