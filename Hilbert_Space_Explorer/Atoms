$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Atoms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Define the set of atoms in a Hilbert lattice.  An atom is a non-zero
       element of a lattice such that anything less than it is zero, i.e. it
       is a smallest non-zero element of the lattice.  Definition of atom in
       [Kalmbach] p. 15.  See ~ elat and ~ elat2 for membership relations. $)
    df-at $a |- Atoms = { x e. CH | 0H <o x } $.
  $}

  ${
    $d x y A $.  $d x B $.
    $( Atoms in a Hilbert lattice are the elements that cover the zero
       subspace.  Definition of atom in [Kalmbach] p. 15. $)
    elat $p |- ( A e. Atoms <-> ( A e. CH /\ 0H <o A ) ) $=
      ( vx c0h cv ccv wbr cch cat breq2 df-at elrab2 ) CBDZEFCAEFBAGHLACEIBJK
      $.
      $( [12-Jun-2004] $) $( [9-Jun-2004] $)

    $( Expanded membership relation for the set of atoms, i.e. the predicate
       "is an atom (of the Hilbert lattice)."  An atom is a non-zero element of
       a lattice such that anything less than it is zero, i.e. it is a smallest
       non-zero element of the lattice. $)
    elat2 $p |- ( A e. Atoms <-> ( A e. CH /\ ( A =/= 0H /\
                 A. x e. CH ( x (_ A -> ( x = A \/ x = 0H ) ) ) ) ) $=
      ( cat wcel cch c0h ccv wbr wa wne cv wss wceq wo wi wral elat wpss wb
      h0elch cvbr2t mpan ch0psst imbi1d imbi2d impexp bi2.04 bitr wn df-or
      orcom df-ne imbi1i 3bitr4 imbi2i 3bitr4g ralbiia a1i anbi12d bitr2d
      pm5.32i bitr4 ) BCDBEDZFBGHZIVCBFJZAKZBLZVFBMZVFFMZNZOZAEPZIZIBQVCVMVDVCV
      DFBRZFVFRZVGIVHOZAEPZIZVMFEDVCVDVRSTAFBUAUBVCVNVEVQVLBUCVQVLSVCVPVKAEVFED
      ZVGVOVHOZOZVGVFFJZVHOZOVPVKVSVTWCVGVSVOWBVHVFUCUDUEVPVOVGVHOOWAVOVGVHUFVO
      VGVHUGUHVJWCVGVIVHNVIUIZVHOVJWCVIVHUJVHVIUKWBWDVHVFFULUMUNUOUPUQURUSUTVAV
      B $.
      $( [18-Jun-2006] $) $( [9-Jun-2004] $)

    $( Expanded membership relation for the set of atoms, i.e. the predicate
       "is an atom (of the Hilbert lattice)."  An atom is a non-zero element of
       a lattice such that anything less than it is zero, i.e. it is a smallest
       non-zero element of the lattice. $)
    elat2OLD $p |- ( A e. Atoms <-> ( A e. CH /\ ( -. A = 0H /\
                 A. x e. CH ( x (_ A -> ( x = A \/ x = 0H ) ) ) ) ) $=
      ( cat wcel cch c0h ccv wbr wa wceq wn cv wss wo wi wral elat wpss wb
      h0elch cvbr2t mpan ch0psstOLD imbi1d imbi2d impexp bi2.04 bitr orcom
      df-or imbi2i 3bitr4g ralbiia a1i anbi12d bitr2d pm5.32i bitr4 ) BCDBEDZFB
      GHZIUSBFJKZALZBMZVBBJZVBFJZNZOZAEPZIZIBQUSVIUTUSUTFBRZFVBRZVCIVDOZAEPZIZV
      IFEDUSUTVNSTAFBUAUBUSVJVAVMVHBUCVMVHSUSVLVGAEVBEDZVCVKVDOZOZVCVEKZVDOZOVL
      VGVOVPVSVCVOVKVRVDVBUCUDUEVLVKVCVDOOVQVKVCVDUFVKVCVDUGUHVFVSVCVFVEVDNVSVD
      VEUIVEVDUJUHUKULUMUNUOUPUQUR $.
      $( [12-Jun-2004] $) $( [9-Jun-2004] $)

    $( A Hilbert lattice element is an atom iff it covers the zero subspace. $)
    elatcv0 $p |- ( A e. CH -> ( A e. Atoms <-> 0H <o A ) ) $=
      ( cat wcel cch c0h ccv wbr elat baib ) ABCADCEAFGAHI $.
      $( [14-Feb-2005] $) $( [26-Jun-2004] $)

    $( An atom covers the zero subspace. $)
    atcv0 $p |- ( A e. Atoms -> 0H <o A ) $=
      ( cat wcel cch c0h ccv wbr elat pm3.27bd ) ABCADCEAFGAHI $.
      $( [22-Jul-2004] $) $( [26-Jun-2004] $)

    $( Atoms are a subset of the Hilbert lattice. $)
    atssch $p |- Atoms (_ CH $=
      ( vx cat c0h cv ccv wbr cch crab df-at ssrab2 eqsstr ) BCADEFZAGHGAILAGJK
      $.
      $( [14-Aug-2002] $)

    $( An atom is a Hilbert lattice element. $)
    atelch $p |- ( A e. Atoms -> A e. CH ) $=
      ( cat cch atssch sseli ) BCADE $.
      $( [22-Jun-2004] $) $( [22-Jun-2004] $)

    $( An atom is not the Hilbert lattice zero. $)
    atn0 $p |- ( A e. Atoms -> A =/= 0H ) $=
      ( vx cat wcel cch c0h wne cv wss wceq wo wi wral wa elat2 simprl sylbi )
      ACDAEDZAFGZBHZAITAJTFJKLBEMZNNSBAORSUAPQ $.
      $( [27-Jun-2006] $) $( [13-Aug-2002] $)

    $( An atom is not the Hilbert lattice zero. $)
    atn0OLD $p |- ( A e. Atoms -> -. A = 0H ) $=
      ( vx cat wcel cch c0h wceq wn cv wss wo wi wral wa elat2OLD simprl sylbi
      ) ACDAEDZAFGHZBIZAJTAGTFGKLBEMZNNSBAORSUAPQ $.
      $( [13-Aug-2002] $)

    $( A lattice element smaller than an atom is either the atom or zero. $)
    atss $p |- ( ( A e. CH /\ B e. Atoms ) ->
               ( A (_ B -> ( A = B \/ A = 0H ) ) ) $=
      ( vx cch wcel c0h wceq wn cv wss wo wi wral wa cat sseq1 eqeq1 orbi12d
      imbi12d rcla4v adantld imp elat2OLD sylan2b ) ADEZBDEZBFGHZCIZBJZUHBGZUHF
      GZKZLZCDMZNZNZABJZABGZAFGZKZLZBOEUEUPVAUEUOVAUFUEUNVAUGUMVACADUHAGZUIUQUL
      UTUHABPVBUJURUKUSUHABQUHAFQRSTUAUAUBCBUCUD $.
      $( [23-Jul-2004] $) $( [25-Jun-2004] $)
  $}

  $( Two atoms in a subset relationship are equal. $)
  atsseq $p |- ( ( A e. Atoms /\ B e. Atoms ) -> ( A (_ B <-> A = B ) ) $=
    ( cat wcel wa wss wceq c0h wn atn0OLD ad2antrr wo cch wi atss atelch sylan
    imp ord mt3d ex eqimss impbid1 ) ACDZBCDZEZABFZABGZUFUGUHUFUGEZUHAHGZUDUJIU
    EUGAJKUIUHUJUFUGUHUJLZAMDUEUGUKNUDABOAPQRSTUAABUBUC $.
    $( [23-Jul-2004] $) $( [26-Jun-2004] $)

  $( A Hilbert lattice element covered by an atom must be the zero subspace. $)
  atcveq0 $p |- ( ( A e. CH /\ B e. Atoms ) -> ( A <o B <-> A = 0H ) ) $=
    ( cch wcel cat wa ccv wbr c0h wceq wss wpss wi cvpsst atelch sylan2 ch0let
    adantr jctild atcv0 h0elch cvnbtwn3t mp3an1 sylan mpd ancoms syld breq1
    syl5bir com12 adantl impbid ) ACDZBEDZFZABGHZAIJZUOUPIAKZABLZFZUQUOUPUSURUM
    BCDZUPUSMUNABNBOZPUMURUNAQRSUNUMUTUQMZUNUMFIBGHZVCUNVDUMBTZRVAUMVDVCMZUNICD
    VAUMVFUAIBAUBUCVBUDUEUFUGUNUQUPMUMUQUNUPUQUPVDUNAIBGUHVEUIUJUKUL $.
    $( [23-Jul-2004] $) $( [11-Jun-2004] $)

  ${
    $d x A $.
    $( A 1-dimensional subspace is an atom. $)
    h1dat $p |- ( ( A e. H~ /\ A =/= 0h ) ->
                ( _|_ ` ( _|_ ` { A } ) ) e. Atoms ) $=
      ( vx chil wcel c0v wne wa csn cort cfv cch c0h wceq wn cv wss wo wi wral
      cat snssi occlt chocclt 3syl adantr h1dn0OLD h1datomt expcom r19.21aiv
      jca elat2OLD sylibr ) ACDZAEFZGZAHZIJZIJZKDZURLMNZBOZURPVAURMVALMQRZBKSZG
      ZGURTDUOUSVDUMUSUNUMUPCPUQKDUSACUAUPUBUQUCUDUEUOUTVCAUFUMVCUNUMVBBKVAKDUM
      VBVAAUGUHUIUEUJUJBURUKUL $.
      $( [22-Jul-2001] $)

    $( A 1-dimensional subspace is an atom. $)
    h1datOLD $p |- ( ( A e. H~ /\ -. A = 0h ) ->
                ( _|_ ` ( _|_ ` { A } ) ) e. Atoms ) $=
      ( vx chil wcel c0v wceq wn wa csn cort cfv cch c0h cv wss wo wi wral cat
      snssi occlt chocclt 3syl adantr h1dn0OLDOLD h1datomt expcom r19.21aiv
      jca elat2OLD sylibr ) ACDZAEFGZHZAIZJKZJKZLDZUQMFGZBNZUQOUTUQFUTMFPQZBLRZ
      HZHUQSDUNURVCULURUMULUOCOUPLDURACTUOUAUPUBUCUDUNUSVBAUEULVBUMULVABLUTLDUL
      VAUTAUFUGUHUDUIUIBUQUJUK $.
      $( [22-Jul-2001] $)
  $}

  $( The span of the singleton of a vector is an atom. $)
  spansnat $p |- ( ( A e. H~ /\ A =/= 0h ) -> ( span ` { A } ) e. Atoms ) $=
    ( chil wcel c0v wne wa csn cspn cfv cort cat wceq spansnt adantr h1dat
    eqeltrd ) ABCZADEZFAGZHIZSJIJIZKQTUALRAMNAOP $.
    $( [20-Jun-2006] $) $( [18-Dec-2004] $)

  $( The span of a singleton is an atom. $)
  spansnatOLD $p |- ( ( A e. H~ /\ -. A = 0h ) ->
                    ( span ` { A } ) e. Atoms ) $=
    ( chil wcel c0v wceq wn wa csn cspn cfv cort cat spansnt adantr h1datOLD
    eqeltrd ) ABCZADEFZGAHZIJZSKJKJZLQTUAERAMNAOP $.
    $( [16-Jan-2005] $) $( [18-Dec-2004] $)

  $( A 1-dimensional subspace is less than or equal to any subspace
     containing its generating vector. $)
  sh1dle $p |- ( ( A e. SH /\ B e. A ) -> ( _|_ ` ( _|_ ` { B } ) ) (_ A ) $=
    ( csh wcel wa csn cort cfv cspn chil wceq shelt spansnt syl spansnsst
    eqsstr3d ) ACDBADEZBFZGHGHZRIHZAQBJDTSKBALBMNABOP $.
    $( [26-Nov-2004] $) $( [24-Nov-2004] $)

  $( A 1-dimensional subspace is less than or equal to any member of ` CH `
     containing its generating vector. $)
  ch1dle $p |- ( ( A e. CH /\ B e. A ) -> ( _|_ ` ( _|_ ` { B } ) ) (_ A ) $=
    ( csh wcel csn cort cfv wss cch sh1dle chsh sylan ) ACDBADBEFGFGAHAIDABJAKL
    $.
    $( [31-May-2004] $) $( [30-May-2004] $)

  ${
    $d x y z A $.
    $( The 1-dimensional subspaces of Hilbert space are its atoms.  Part of
       Remark 10.3.5 of [BeltramettiCassinelli] p. 107. $)
    atom1d $p |- ( A e. Atoms <->
                E. x e. H~ ( x =/= 0h /\ A = ( span ` { x } ) ) ) $=
      ( vy cat wcel cv c0v wne csn cort cfv wceq wa chil wrex cspn cch c0h wss
      wo wi wral elat2 chne0t ax-17 hbre1 hbim ra4e chelt adantrr pm3.27
      ad2antrl h1dn0 sylan anasss wn ch1dle snssi occlt 3syl chocclt sseq1
      eqeq1 orbi12d imbi12d rcla4v mpid ex imp32 adantrlr ord nne syl6ibr mt4d
      eqcomd jca sylanc exp44 r19.23ad sylbid sylbi eleq1 h1dat syl5bir exp3a
      com3l imp3a r19.23aiv impbi spansnt eqeq2d anbi2d rexbiia bitr4 ) BDEZAFZ
      GHZBWPIZJKZJKZLZMZANOZWQBWRPKZLZMZANOWOXCWOBQEZBRHZCFZBSZXIBLZXIRLZTZUAZC
      QUBZMMXCCBUCXGXHXOXCXGXHWQABOXOXCUAZABUDXGWQXPABXGAUEXOXCAXOAUEXBANUFUGXG
      WPBEZWQXOXCWPNEZXBXCXGXQWQMZXOMMZXBANUHXGXSXRXOXGXQXRWQWPBUIZUJUJXTWQXAXS
      WQXGXOXQWQUKULXTWTBXTWTRHZWTBLZXGXSYBXOXGXQWQYBXRWQYBXGXQMZWPUMYAUNUOUJXT
      YCUPWTRLZYBUPXTYCYEXGXQXOYCYETZWQXGXQXOYFXGXQXOYFUAYDXOWTBSZYFBWPUQYDWSQE
      ZWTQEXOYGYFUAZUAYDXRWRNSYHYAWPNURWRUSUTWSVAXNYICWTQXIWTLZXJYGXMYFXIWTBVBY
      JXKYCXLYEXIWTBVCXIWTRVCVDVEVFUTVGVHVIVJVKWTRVLVMVNVOVPVQVRVSVTVIWAXBWOANX
      RWQXAWOXAXRWQWOXAXRWQWOXAWOWTDEXRWQMBWTDWBWPWCWDWEWFWGWHWIXFXBANXRXEXAWQX
      RXDWTBWPWJWKWLWMWN $.
      $( [25-Jun-2006] $) $( [4-Jun-2004] $)
  $}

  ${
    $d x y z A $.
    $( The 1-dimensional subspaces of Hilbert space are its atoms.  Part of
       Remark 10.3.5 of [BeltramettiCassinelli] p. 107. $)
    atom1dOLD $p |- ( A e. Atoms <->
                E. x e. H~ ( -. x = 0h /\ A = ( span ` { x } ) ) ) $=
      ( vy cat wcel cv c0v wceq wn csn cort cfv wa chil wrex cspn cch c0h wss
      wo wi wral elat2OLD chne0tOLD ax-17 hbre1 hbim ra4e chelt adantrr pm3.27
      ad2antrl h1dn0OLDOLD sylan anasss ch1dle snssi occlt syl chocclt 3syl
      sseq1 eqeq1 orbi12d imbi12d rcla4v mpid ex imp32 adantrlr ord mt3d
      eqcomd jca sylanc exp44 r19.23ad sylbid sylbi eleq1 h1datOLD syl5bir
      exp3a com3l imp3a r19.23aiv impbi spansnt eqeq2d anbi2d rexbiia bitr4 )
      BDEZAFZGHIZBWNJZKLZKLZHZMZANOZWOBWPPLZHZMZANOWMXAWMBQEZBRHIZCFZBSZXGBHZXG
      RHZTZUAZCQUBZMMXACBUCXEXFXMXAXEXFWOABOXMXAUAZABUDXEWOXNABXEAUEXMXAAXMAUEW
      TANUFUGXEWNBEZWOXMXAWNNEZWTXAXEXOWOMZXMMMZWTANUHXEXQXPXMXEXOXPWOWNBUIZUJU
      JXRWOWSXQWOXEXMXOWOUKULXRWRBXRWRBHZWRRHZXEXQYAIZXMXEXOWOYBXPWOYBXEXOMZWNU
      MXSUNUOUJXRXTYAXEXOXMXTYATZWOXEXOXMYDXEXOXMYDUAYCXMWRBSZYDBWNUPYCWRQEZXMY
      EYDUAZUAYCXPWQQEZYFXSXPWPNSYHWNNUQWPURUSWQUTVAXLYGCWRQXGWRHZXHYEXKYDXGWRB
      VBYIXIXTXJYAXGWRBVCXGWRRVCVDVEVFUSVGVHVIVJVKVLVMVNVOVPVQVRVIVSWTWMANXPWOW
      SWMWSXPWOWMWSXPWOWMWSWMWRDEXPWOMBWRDVTWNWAWBWCWDWEWFWGXDWTANXPXCWSWOXPXBW
      RBWNWHWIWJWKWL $.
      $( [23-Jul-2004] $) $( [4-Jun-2004] $)
  $}

