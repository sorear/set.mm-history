$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Commutes relation for Hilbert lattice elements
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Define the commutes relation (on the Hilbert lattice).  Definition of
       commutes in [Kalmbach] p. 20, who uses the notation xCy for "x commutes
       with y."  See ~ cmbri for membership relation. $)
    df-cm $a |- C_H = { <. x , y >. | ( ( x e. CH /\ y e. CH ) /\
                x = ( ( x i^i y ) vH ( x i^i ( _|_ ` y ) ) ) ) } $.
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Binary relation expressing ` A ` commutes with ` B ` .  Definition of
       commutes in [Kalmbach] p. 20. $)
    cmbr $p |- ( ( A e. CH /\ B e. CH ) -> ( A C_H B <->
               A = ( ( A i^i B ) vH ( A i^i ( _|_ ` B ) ) ) ) ) $=
      ( vx vy cch wcel wa ccm wbr cin cort cfv chj co wceq cv eleq1 anbi1d id
      ineq1 opreq12d eqeq12d anbi12d anbi2d ineq2 fveq2 ineq2d eqeq2d df-cm
      brabg bianabs ) AEFZBEFZGZABHIAABJZABKLZJZMNZOZCPZEFZDPZEFZGZUTUTVBJZUTVB
      KLZJZMNZOZGULVCGZAAVBJZAVFJZMNZOZGUNUSGCDABEEHUTAOZVDVJVIVNVOVAULVCUTAEQR
      VOUTAVHVMVOSVOVEVKVGVLMUTAVBTUTAVFTUAUBUCVBBOZVJUNVNUSVPVCUMULVBBEQUDVPVM
      URAVPVKUOVLUQMVBBAUEVPVFUPAVBBKUFUGUAUHUCCDUIUJUK $.
      $( [12-Aug-2004] $) $( [14-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    pjoml2.1 $e |- A e. CH $.
    pjoml2.2 $e |- B e. CH $.
    $( Variation of orthomodular law.  Definition in [Kalmbach] p. 22. $)
    pjoml2i $p |- ( A C_ B -> ( A vH ( ( _|_ ` A ) i^i B ) ) = B ) $=
      ( wss cort cfv cin chj co wceq inss2 choccli chincli chlubii mpan2 c0h
      chdmj1i ineq2i inass incom ineq1i eqtr3i chocini 3eqtri chjcli chshii
      pjomli syl ) ABEZAAFGZBHZIJZBEZUMBKZUJULBEUNUKBLAULBCUKBACMDNZDOPUNBUMFGZ
      HZQKUOURBUKULFGZHZHZULUSHZQUQUTBAULCUPRSBUKHZUSHVAVBBUKUSTVCULUSBUKUAUBUC
      ULUPUDUEUMBAULCUPUFBDUGUHPUI $.
      $( [31-Oct-2000] $)

    $( Variation of orthomodular law. $)
    pjoml3i $p |- ( B C_ A -> ( A i^i ( ( _|_ ` A ) vH B ) ) = B ) $=
      ( cort cfv wss cin chj co wceq choccli pjoml2i chsscon3i eqcom chincli
      chdmj2i chdmm4i ineq2i eqtri eqeq1i chjcli chcon2i 3bitr3i 3imtr4i ) AEFZ
      BEFZGUFUFEFZUGHZIJZUGKZBAGAUFBIJZHZBKZUFUGACLZBDLZMBADCNUJEFZBKBUQKUNUKUQ
      BOUQUMBUQAUIEFZHUMAUICUHUGUFUOLUPPZQURULAUFBUODRSTUABUJDUFUIUOUSUBUCUDUE
      $.
      $( [9-Sep-2004] $) $( [24-Jun-2004] $)

    $( Variation of orthomodular law. $)
    pjoml4i $p |- ( A vH ( B i^i ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) ) =
                 ( A vH B ) $=
      ( cort cfv chj co cin wss inss1 choccli chjcli chincli chlej2i ax-mp
      chub1i chdmm1i ineq1i incom eqtri opreq2i wceq inss2 pjoml2i eqtr3i
      chlej1i eqsstr3i chlubii mp2an eqssi ) ABAEFZBEFZGHZIZGHZABGHZUOBJUPUQJBU
      NKUOBABUNDULUMACLBDLMNZDCOPAUPJBUPJUQUPJAUOCURQBABIZUOGHZUPUSUSEFZBIZGHZU
      TBVBUOUSGVBUNBIUOVAUNBABCDRSUNBTUAUBUSBJVCBUCABUDUSBABCDNZDUEPUFUSAJUTUPJ
      ABKUSAUOVDCURUGPUHABUPCDAUOCURMUIUJUK $.
      $( [6-Dec-2000] $)

    $( The orthomodular law.  Remark in [Kalmbach] p. 22. $)
    pjoml5i $p |- ( A vH ( ( _|_ ` A ) i^i ( A vH B ) ) ) = ( A vH B ) $=
      ( chj co wss cort cfv cin wceq chub1i chjcli pjoml2i ax-mp ) AABEFZGAAHIP
      JEFPKABCDLAPCABCDMNO $.
      $( [12-Jun-2006] $)

    $( An equivalent of the orthomodular law.  Theorem 29.13(e) of [MaedaMaeda]
       p. 132. $)
    pjoml6i $p |- ( A C_ B ->
                  E. x e. CH ( A C_ ( _|_ ` x ) /\ ( A vH x ) = B ) ) $=
      ( wss cort cfv cin chj co wceq wa cv cch wrex pjoml2i choccli chub1i
      chdmm2i sseqtr4i jctil wcel chincli fveq2 sseq2d opreq2 eqeq1d anbi12d
      rcla4ev mpan syl ) BCFZBBGHZCIZGHZFZBUOJKZCLZMZBANZGHZFZBVAJKZCLZMZAOPZUM
      USUQBCDEQBBCGHZJKUPBVHDCERSBCDETUAUBUOOUCUTVGUNCBDREUDVFUTAUOOVAUOLZVCUQV
      EUSVIVBUPBVAUOGUEUFVIVDURCVAUOBJUGUHUIUJUKUL $.
      $( [9-Sep-2004] $) $( [30-May-2004] $)

    $( Binary relation expressing the commutes relation.  Definition of
       commutes in [Kalmbach] p. 20. $)
    cmbri $p |- ( A C_H B <-> A = ( ( A i^i B ) vH ( A i^i ( _|_ ` B ) ) ) ) $=
      ( cch wcel ccm wbr cin cort cfv chj co wceq wb cmbr mp2an ) AEFBEFABGHAAB
      IABJKILMNOCDABPQ $.
      $( [12-Aug-2004] $) $( [6-Aug-2004] $)

    $( Commutation is symmetric.  Theorem 3.4 of [Beran] p. 45. $)
    cmcmlem $p |- ( A C_H B -> B C_H A ) $=
      ( cin cort cfv chj co wceq ccm wbr chdmj4i chdmj2i opreq12i eqeq2i
      biimpri fveq2d choccli chjcli syl6req ineq1d wss chub2i sseqin2 mpbi
      ineq2i inass chdmm1i ineq1i 3eqtr4ri syl5eq opreq2d inss2 chincli
      pjoml2i ax-mp incom 3eqtr3g cmbri 3imtr4i ) AABEZABFGZEZHIZJZBBAEZBAFGZEZ
      HIZJABKLBAKLVFVBVBFGZBEZHIZVBVHBEZHIBVJVFVLVNVBHVFVHVCHIZVHBHIZEZBEZVNVLV
      FVQVHBVFVHVOFGZVPFGZHIZFGVQVFAWAFAWAJVFWAVEAVSVBVTVDHABCDMABCDNOPQRVOVPVH
      VCACSZBDSTVHBWBDTMUAUBVOVPBEZEVOBEVRVLWCBVOBVPUCWCBJBVHDWBUDBVPUEUFUGVOVP
      BUHVKVOBABCDUIUJUKULUMVBBUCVMBJABUNVBBABCDUODUPUQVBVGVNVIHABURVHBUROUSABC
      DUTBADCUTVA $.
      $( [12-Aug-2004] $) $( [3-Nov-2000] $)

    $( Commutation is symmetric.  Theorem 2(v) of [Kalmbach] p. 22. $)
    cmcmi $p |- ( A C_H B <-> B C_H A ) $=
      ( ccm wbr cmcmlem impbii ) ABEFBAEFABCDGBADCGH $.
      $( [12-Aug-2004] $) $( [4-Nov-2000] $)

    $( Commutation with orthocomplement.  Theorem 2.3(i) of [Beran] p. 39. $)
    cmcm2i $p |- ( A C_H B <-> A C_H ( _|_ ` B ) ) $=
      ( cin cort cfv chj co wceq ccm wbr chincli choccli chjcomi pjococi
      ineq2i opreq2i eqtr4i eqeq2i cmbri 3bitr4i ) AABEZABFGZEZHIZJAUEAUDFGZEZH
      IZJABKLAUDKLUFUIAUFUEUCHIUIUCUEABCDMAUDCBDNZMOUHUCUEHUGBABDPQRSTABCDUAAUD
      CUJUAUB $.
      $( [12-Aug-2004] $) $( [4-Nov-2000] $)

    $( Commutation with orthocomplement.  Remark in [Kalmbach] p. 23. $)
    cmcm3i $p |- ( A C_H B <-> ( _|_ ` A ) C_H B ) $=
      ( ccm wbr cort cfv cmcm2i cmcmi choccli 3bitr4i ) BAEFBAGHZEFABEFMBEFBADC
      IABCDJMBACKDJL $.
      $( [12-Aug-2004] $) $( [4-Nov-2000] $)

    $( Commutation with orthocomplement.  Remark in [Kalmbach] p. 23. $)
    cmcm4i $p |- ( A C_H B <-> ( _|_ ` A ) C_H ( _|_ ` B ) ) $=
      ( ccm wbr cort cfv cmcm2i choccli cmcm3i bitri ) ABEFABGHZEFAGHMEFABCDIAM
      CBDJKL $.
      $( [12-Aug-2004] $) $( [7-Aug-2004] $)

    $( Alternate definition of the commutes relation.  Remark in [Kalmbach]
       p. 23. $)
    cmbr2i $p |- ( A C_H B <-> A = ( ( A vH B ) i^i ( A vH ( _|_ ` B ) ) ) ) $=
      ( ccm wbr cort cfv cin chj co wceq cmcm4i choccli cmbri eqcom chjcli
      chincli chcon3i chdmm1i chdmj1i opreq12i eqtri eqeq2i 3bitrri 3bitri ) AB
      EFAGHZBGHZEFUGUGUHIZUGUHGHIZJKZLZAABJKZAUHJKZIZLZABCDMUGUHACNBDNZOUPUOALU
      GUOGHZLULAUOPUOAUMUNABCDQZAUHCUQQZRCSURUKUGURUMGHZUNGHZJKUKUMUNUSUTTVAUIV
      BUJJABCDUAAUHCUQUAUBUCUDUEUF $.
      $( [12-Aug-2004] $) $( [7-Aug-2004] $)

    ${
      cmcmi.1 $e |- A C_H B $.
      $( Commutation is symmetric.  Theorem 2(v) of [Kalmbach] p. 22. $)
      cmcmii $p |- B C_H A $=
        ( ccm wbr cmcmi mpbi ) ABFGBAFGEABCDHI $.
        $( [12-Aug-2004] $) $( [7-Aug-2004] $)

      $( Commutation with orthocomplement.  Theorem 2.3(i) of [Beran] p. 39. $)
      cmcm2ii $p |- A C_H ( _|_ ` B ) $=
        ( ccm wbr cort cfv cmcm2i mpbi ) ABFGABHIFGEABCDJK $.
        $( [12-Aug-2004] $) $( [7-Aug-2004] $)

      $( Commutation with orthocomplement.  Remark in [Kalmbach] p. 23. $)
      cmcm3ii $p |- ( _|_ ` A ) C_H B $=
        ( ccm wbr cort cfv cmcm3i mpbi ) ABFGAHIBFGEABCDJK $.
        $( [13-Sep-2004] $) $( [7-Aug-2004] $)
    $}

    $( Alternate definition for the commutes relation.  Lemma 3 of [Kalmbach]
       p. 23. $)
    cmbr3i $p |- ( A C_H B
             <-> ( A i^i ( ( _|_ ` A ) vH B ) ) = ( A i^i B ) ) $=
      ( ccm wbr cort cfv chj co cin wceq cmcmi cmbr2i bitri ineq2 inass
      chjcomi ineq2i chabs2i eqtri choccli ineq12i eqtr3i syl6req sylbi
      chdmm3i incom eqeq1i opreq1 wss inss1 chincli pjoml2i ax-mp syl5eq cmbri
      sylibr impbii ) ABEFZAAGHZBIJZKZABKZLZUTBBAIJZBVAIJZKZLZVEUTBAEFVIABCDMBA
      DCNOVIVDAVHKZVCBVHAPAVFKZVGKVJVCAVFVGQVKAVGVBVKAABIJZKAVFVLABADCRSABCDTUA
      BVADACUBRUCUDUEUFVEAVDABGHZKZIJZLUTVEVNGHZAKZVNIJZVOAVEVQVDLVRVOLVCVQVDAV
      PKVCVQVPVBAABCDUGSAVPUHUDUIVQVDVNIUJUFVNVQIJZAVRVNAUKVSALAVMULVNAAVMCBDUB
      UMZCUNUOVNVQVTVPAVNVTUBCUMRUDUPABCDUQURUS $.
      $( [12-Aug-2004] $) $( [6-Dec-2000] $)

    $( Alternate definition for the commutes relation. $)
    cmbr4i $p |- ( A C_H B <-> ( A i^i ( ( _|_ ` A ) vH B ) ) C_ B ) $=
      ( ccm wbr cort cfv chj co cin wceq wss cmbr3i inss2 sseq1 mpbiri wa
      inss1 jctl ssin sylib choccli chub2i sslin ax-mp jctir eqss sylibr
      impbii bitri ) ABEFAAGHZBIJZKZABKZLZUNBMZABCDNUPUQUPUQUOBMABOUNUOBPQUQUNU
      OMZUOUNMZRUPUQURUSUQUNAMZUQRURUQUTAUMSTUNABUAUBBUMMUSBULDACUCUDBUMAUEUFUG
      UNUOUHUIUJUK $.
      $( [12-Aug-2004] $) $( [6-Dec-2000] $)

    $( Comparable Hilbert lattice elements commute.  Theorem 2.3(iii) of
       [Beran] p. 40. $)
    lecmi $p |- ( A C_ B -> A C_H B ) $=
      ( wss cort cfv chj co cin ccm wbr ssinss1 cmbr4i sylibr ) ABEAAFGBHIZJBEA
      BKLAPBMABCDNO $.
      $( [22-Jan-2005] $) $( [16-Jan-2005] $)

    ${
      lecmi.1 $e |- A C_ B $.
      $( Comparable Hilbert lattice elements commute.  Theorem 2.3(iii) of
         [Beran] p. 40. $)
      lecmii $p |- A C_H B $=
        ( wss ccm wbr lecmi ax-mp ) ABFABGHEABCDIJ $.
        $( [12-Aug-2004] $) $( [7-Aug-2004] $)
    $}

    $( A Hilbert lattice element commutes with its join. $)
    cmj1i $p |- A C_H ( A vH B ) $=
      ( chj co chjcli chub1i lecmii ) AABEFCABCDGABCDHI $.
      $( [12-Aug-2004] $) $( [7-Aug-2004] $)

    $( A Hilbert lattice element commutes with its join. $)
    cmj2i $p |- B C_H ( A vH B ) $=
      ( chj co chjcli chub2i lecmii ) BABEFDABCDGBADCHI $.
      $( [12-Aug-2004] $) $( [7-Aug-2004] $)

    $( A Hilbert lattice element commutes with its meet. $)
    cmm1i $p |- A C_H ( A i^i B ) $=
      ( cin chincli inss1 lecmii cmcmii ) ABEZAABCDFZCJAKCABGHI $.
      $( [13-Sep-2004] $) $( [7-Aug-2004] $)

    $( A Hilbert lattice element commutes with its meet. $)
    cmm2i $p |- B C_H ( A i^i B ) $=
      ( cin ccm cmm1i incom breqtri ) BBAEABEFBADCGBAHI $.
      $( [13-Sep-2004] $) $( [7-Aug-2004] $)
  $}

  $( Alternate definition for the commutes relation.  Lemma 3 of [Kalmbach]
     p. 23. $)
  cmbr3 $p |- ( ( A e. CH /\ B e. CH ) ->
              ( A C_H B <-> ( A i^i ( ( _|_ ` A ) vH B ) ) = ( A i^i B ) ) ) $=
    ( cch wcel ccm wbr cort cfv chj co cin wceq wb c0h cif breq1 id fveq2
    opreq1d ineq12d ineq1 eqeq12d bibi12d breq2 opreq2 ineq2d ineq2 h0elch
    elimel cmbr3i dedth2h ) ACDZBCDZABEFZAAGHZBIJZKZABKZLZMULANOZBEFZUTUTGHZBIJ
    ZKZUTBKZLZMUTUMBNOZEFZUTVBVGIJZKZUTVGKZLZMABNNAUTLZUNVAUSVFAUTBEPVMUQVDURVE
    VMAUTUPVCVMQVMUOVBBIAUTGRSTAUTBUAUBUCBVGLZVAVHVFVLBVGUTEUDVNVDVJVEVKVNVCVIU
    TBVGVBIUEUFBVGUTUGUBUCUTVGANCUHUIBNCUHUIUJUK $.
    $( [15-Jun-2006] $) $( [14-Jun-2006] $)

  $( The zero Hilbert lattice element commutes with every element. $)
  cm0 $p |- ( A e. CH -> 0H C_H A ) $=
    ( cch wcel c0h ccm wbr cort cfv chj co cin wceq h0elch choccli chjcl mpan
    chm0 syl eqtr4d incom 3eqtr4g wb cmbr3 mpbird ) ABCZDAEFZDDGHZAIJZKZDAKZLZU
    EUHDKZADKZUIUJUEULDUMUEUHBCZULDLUGBCUEUNDMNUGAOPUHQRAQSDUHTDATUADBCUEUFUKUB
    MDAUCPUD $.
    $( [18-Jun-2006] $) $( [16-Jun-2006] $)

  ${
    cmid.1 $e |- A e. CH $.
    $( The commutes relation is reflexive. $)
    cmidi $p |- A C_H A $=
      ( ssid lecmii ) AABBACD $.
      $( [13-Sep-2004] $) $( [7-Aug-2004] $)
  $}

  $( Variation of orthomodular law.  Definition in [Kalmbach] p. 22. $)
  pjoml2 $p |- ( ( A e. CH /\ B e. CH /\ A C_ B ) ->
                ( A vH ( ( _|_ ` A ) i^i B ) ) = B ) $=
    ( cch wcel wss cort cfv cin chj co wceq wi c0h cif sseq1 id fveq2 ineq1d
    opreq12d eqeq1d imbi12d sseq2 ineq2 opreq2d eqeq12d h0elch elimel pjoml2i
    dedth2h 3impia ) ACDZBCDZABEZAAFGZBHZIJZBKZUKULUMUQLUKAMNZBEZURURFGZBHZIJZB
    KZLURULBMNZEZURUTVDHZIJZVDKZLABMMAURKZUMUSUQVCAURBOVIUPVBBVIAURUOVAIVIPVIUN
    UTBAURFQRSTUABVDKZUSVEVCVHBVDURUBVJVBVGBVDVJVAVFURIBVDUTUCUDVJPUEUAURVDAMCU
    FUGBMCUFUGUHUIUJ $.
    $( [27-Jun-2006] $) $( [13-Jun-2006] $)

  $( Variation of orthomodular law. $)
  pjoml3 $p |- ( ( A e. CH /\ B e. CH ) ->
                ( B C_ A -> ( A i^i ( ( _|_ ` A ) vH B ) ) = B ) ) $=
    ( cch wcel wss cort cfv chj co cin wceq wi chil cif sseq2 id fveq2 opreq1d
    ineq12d eqeq1d imbi12d sseq1 opreq2 ineq2d eqeq12d helch elimel pjoml3i
    dedth2h ) ACDZBCDZBAEZAAFGZBHIZJZBKZLBUJAMNZEZUQUQFGZBHIZJZBKZLUKBMNZUQEZUQ
    USVCHIZJZVCKZLABMMAUQKZULURUPVBAUQBOVHUOVABVHAUQUNUTVHPVHUMUSBHAUQFQRSTUABV
    CKZURVDVBVGBVCUQUBVIVAVFBVCVIUTVEUQBVCUSHUCUDVIPUEUAUQVCAMCUFUGBMCUFUGUHUI
    $.
    $( [13-Sep-2004] $) $( [24-Jun-2004] $)

  $( The orthomodular law.  Remark in [Kalmbach] p. 22. $)
  pjoml5 $p |- ( ( A e. CH /\ B e. CH ) ->
             ( A vH ( ( _|_ ` A ) i^i ( A vH B ) ) ) = ( A vH B ) ) $=
    ( cch wcel chj co wss cort cfv cin wceq wa pjoml2 pm3.26 chjcl chub1
    syl3anc ) ACDZABEFZCDASGAAHISJEFSKRBCDZLASMRTNABOABPQ $.
    $( [30-Jun-2006] $) $( [12-Jun-2006] $)

  $( Commutation is symmetric.  Theorem 2(v) of [Kalmbach] p. 22. $)
  cmcm $p |- ( ( A e. CH /\ B e. CH ) -> ( A C_H B <-> B C_H A ) ) $=
    ( cch wcel ccm wbr wb c0h cif wceq breq1 breq2 bibi12d h0elch elimel cmcmi
    dedth2h ) ACDZBCDZABEFZBAEFZGRAHIZBEFZBUBEFZGUBSBHIZEFZUEUBEFZGABHHAUBJTUCU
    AUDAUBBEKAUBBELMBUEJUCUFUDUGBUEUBELBUEUBEKMUBUEAHCNOBHCNOPQ $.
    $( [14-Jun-2006] $) $( [13-Jun-2006] $)

  $( Commutation with orthocomplement.  Remark in [Kalmbach] p. 23. $)
  cmcm3 $p |- ( ( A e. CH /\ B e. CH ) ->
               ( A C_H B <-> ( _|_ ` A ) C_H B ) ) $=
    ( cch wcel ccm wbr cort cfv wb c0h cif wceq breq1 fveq2 breq1d bibi12d
    breq2 h0elch elimel cmcm3i dedth2h ) ACDZBCDZABEFZAGHZBEFZIUBAJKZBEFZUGGHZB
    EFZIUGUCBJKZEFZUIUKEFZIABJJAUGLZUDUHUFUJAUGBEMUNUEUIBEAUGGNOPBUKLUHULUJUMBU
    KUGEQBUKUIEQPUGUKAJCRSBJCRSTUA $.
    $( [14-Jun-2006] $) $( [13-Jun-2006] $)

  $( Commutation with orthocomplement.  Theorem 2.3(i) of [Beran] p. 39. $)
  cmcm2 $p |- ( ( A e. CH /\ B e. CH ) ->
               ( A C_H B <-> A C_H ( _|_ ` B ) ) ) $=
    ( cch wcel wa ccm wbr cort cfv wb cmcm3 ancoms cmcm choccl sylan2 3bitr4d
    ) ACDZBCDZEBAFGZBHIZAFGZABFGATFGZRQSUAJBAKLABMQTCDUBUAJRATMBNOP $.
    $( [14-Jun-2006] $)

  $( Comparable Hilbert lattice elements commute.  Theorem 2.3(iii) of [Beran]
     p. 40. $)
  lecm $p |- ( ( A e. CH /\ B e. CH /\ A C_ B ) -> A C_H B ) $=
    ( cch wcel wss ccm wbr wi c0h cif wceq sseq1 breq1 imbi12d sseq2 breq2
    h0elch elimel lecmi dedth2h 3impia ) ACDZBCDZABEZABFGZUBUCUDUEHUBAIJZBEZUFB
    FGZHUFUCBIJZEZUFUIFGZHABIIAUFKUDUGUEUHAUFBLAUFBFMNBUIKUGUJUHUKBUIUFOBUIUFFP
    NUFUIAICQRBICQRSTUA $.
    $( [20-Jun-2006] $) $( [13-Jun-2006] $)

