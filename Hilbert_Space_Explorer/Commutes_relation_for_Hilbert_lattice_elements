$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Commutes relation for Hilbert lattice elements
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Define the commutes relation (on the Hilbert lattice).  Definition of
       commutes in [Kalmbach] p. 20, who uses the notation xCy for "x commutes
       with y."  See ~ cmbr for membership relation. $)
    df-cm $a |- C_H = { <. x , y >. | ( ( x e. CH /\ y e. CH ) /\
                x = ( ( x i^i y ) vH ( x i^i ( _|_ ` y ) ) ) ) } $.
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Binary relation expressing ` A ` commutes with ` B ` .  Definition of
       commutes in [Kalmbach] p. 20. $)
    cmbrt $p |- ( ( A e. CH /\ B e. CH ) -> ( A C_H B <->
               A = ( ( A i^i B ) vH ( A i^i ( _|_ ` B ) ) ) ) ) $=
      ( vx vy cch wcel wa ccm wbr cin cort cfv chj co wceq cv eleq1 anbi1d id
      ineq1 opreq12d eqeq12d anbi12d anbi2d ineq2 fveq2 ineq2d eqeq2d df-cm
      brabg ompfl2 ) AEFZBEFZGZABHIAABJZABKLZJZMNZOZCPZEFZDPZEFZGZUTUTVBJZUTVBK
      LZJZMNZOZGULVCGZAAVBJZAVFJZMNZOZGUNUSGCDABEEHUTAOZVDVJVIVNVOVAULVCUTAEQRV
      OUTAVHVMVOSVOVEVKVGVLMUTAVBTUTAVFTUAUBUCVBBOZVJUNVNUSVPVCUMULVBBEQUDVPVMU
      RAVPVKUOVLUQMVBBAUEVPVFUPAVBBKUFUGUAUHUCCDUIUJUK $.
      $( [12-Aug-2004] $) $( [14-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    pjoml2.1 $e |- A e. CH $.
    pjoml2.2 $e |- B e. CH $.
    $( Variation of orthomodular law.  Definition in [Kalmbach] p. 22. $)
    pjoml2 $p |- ( A (_ B -> ( A vH ( ( _|_ ` A ) i^i B ) ) = B ) $=
      ( wss cort cfv cin chj co wceq inss2 choccl chincl chlubi mpan2 c0h
      chdmj1 ineq2i inass incom ineq1i eqtr3 chocin 3eqtr chjcl chshi pjoml
      syl ) ABEZAAFGZBHZIJZBEZUMBKZUJULBEUNUKBLAULBCUKBACMDNZDOPUNBUMFGZHZQKUOU
      RBUKULFGZHZHZULUSHZQUQUTBAULCUPRSBUKHZUSHVAVBBUKUSTVCULUSBUKUAUBUCULUPUDU
      EUMBAULCUPUFBDUGUHPUI $.
      $( [31-Oct-2000] $)

    $( Variation of orthomodular law. $)
    pjoml3 $p |- ( B (_ A -> ( A i^i ( ( _|_ ` A ) vH B ) ) = B ) $=
      ( cort cfv wss cin chj co wceq choccl pjoml2 chsscon3 eqcom chincl
      chdmj2 chdmm4 ineq2i eqtr eqeq1i chjcl chcon2 3bitr3 3imtr4 ) AEFZBEFZGUF
      UFEFZUGHZIJZUGKZBAGAUFBIJZHZBKZUFUGACLZBDLZMBADCNUJEFZBKBUQKUNUKUQBOUQUMB
      UQAUIEFZHUMAUICUHUGUFUOLUPPZQURULAUFBUODRSTUABUJDUFUIUOUSUBUCUDUE $.
      $( [9-Sep-2004] $) $( [24-Jun-2004] $)

    $( Variation of orthomodular law. $)
    pjoml4 $p |- ( A vH ( B i^i ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) ) =
                 ( A vH B ) $=
      ( cort cfv chj co cin wss inss1 choccl chjcl chincl chlej2 ax-mp chub1
      chdmm1 ineq1i incom eqtr opreq2i wceq inss2 pjoml2 eqtr3 chlej1 eqsstr3
      chlubi mp2an eqssi ) ABAEFZBEFZGHZIZGHZABGHZUOBJUPUQJBUNKUOBABUNDULUMACLB
      DLMNZDCOPAUPJBUPJUQUPJAUOCURQBABIZUOGHZUPUSUSEFZBIZGHZUTBVBUOUSGVBUNBIUOV
      AUNBABCDRSUNBTUAUBUSBJVCBUCABUDUSBABCDNZDUEPUFUSAJUTUPJABKUSAUOVDCURUGPUH
      ABUPCDAUOCURMUIUJUK $.
      $( [6-Dec-2000] $)

    $( The orthomodular law.  Remark in [Kalmbach] p. 22. $)
    pjoml5 $p |- ( A vH ( ( _|_ ` A ) i^i ( A vH B ) ) ) = ( A vH B ) $=
      ( chj co wss cort cfv cin wceq chub1 chjcl pjoml2 ax-mp ) AABEFZGAAHIPJEF
      PKABCDLAPCABCDMNO $.
      $( [12-Jun-2006] $) $( [12-Jun-2006] $)

    $( An equivalent of the orthomodular law.  Theorem 29.13(e) of [MaedaMaeda]
       p. 132. $)
    pjoml6 $p |- ( A (_ B ->
                  E. x e. CH ( A (_ ( _|_ ` x ) /\ ( A vH x ) = B ) ) $=
      ( wss cort cfv cin chj co wceq wa cv cch wrex pjoml2 choccl chub1 chdmm2
      sseqtr4 jctil wcel chincl fveq2 sseq2d opreq2 eqeq1d anbi12d rcla4ev
      mpan syl ) BCFZBBGHZCIZGHZFZBUOJKZCLZMZBANZGHZFZBVAJKZCLZMZAOPZUMUSUQBCDE
      QBBCGHZJKUPBVHDCERSBCDETUAUBUOOUCUTVGUNCBDREUDVFUTAUOOVAUOLZVCUQVEUSVIVBU
      PBVAUOGUEUFVIVDURCVAUOBJUGUHUIUJUKUL $.
      $( [9-Sep-2004] $) $( [30-May-2004] $)

    $( Binary relation expressing the commutes relation.  Definition of
       commutes in [Kalmbach] p. 20. $)
    cmbr $p |- ( A C_H B <-> A = ( ( A i^i B ) vH ( A i^i ( _|_ ` B ) ) ) ) $=
      ( cch wcel ccm wbr cin cort cfv chj co wceq wb cmbrt mp2an ) AEFBEFABGHAA
      BIABJKILMNOCDABPQ $.
      $( [12-Aug-2004] $) $( [6-Aug-2004] $)

    $( Commutation is symmetric.  Theorem 3.4 of [Beran] p. 45. $)
    cmcmlem $p |- ( A C_H B -> B C_H A ) $=
      ( cin cort cfv chj co wceq ccm wbr chdmj4 chdmj2 opreq12i eqeq2i biimpr
      fveq2d choccl chjcl syl6req ineq1d wss chub2 sseqin2 mpbi ineq2i inass
      chdmm1 ineq1i 3eqtr4r syl5eq opreq2d inss2 chincl pjoml2 ax-mp incom
      3eqtr3g cmbr 3imtr4 ) AABEZABFGZEZHIZJZBBAEZBAFGZEZHIZJABKLBAKLVFVBVBFGZB
      EZHIZVBVHBEZHIBVJVFVLVNVBHVFVHVCHIZVHBHIZEZBEZVNVLVFVQVHBVFVHVOFGZVPFGZHI
      ZFGVQVFAWAFAWAJVFWAVEAVSVBVTVDHABCDMABCDNOPQRVOVPVHVCACSZBDSTVHBWBDTMUAUB
      VOVPBEZEVOBEVRVLWCBVOBVPUCWCBJBVHDWBUDBVPUEUFUGVOVPBUHVKVOBABCDUIUJUKULUM
      VBBUCVMBJABUNVBBABCDUODUPUQVBVGVNVIHABURVHBUROUSABCDUTBADCUTVA $.
      $( [12-Aug-2004] $) $( [3-Nov-2000] $)

    $( Commutation is symmetric.  Theorem 2(v) of [Kalmbach] p. 22. $)
    cmcm $p |- ( A C_H B <-> B C_H A ) $=
      ( ccm wbr cmcmlem impbi ) ABEFBAEFABCDGBADCGH $.
      $( [12-Aug-2004] $) $( [4-Nov-2000] $)

    $( Commutation with orthocomplement.  Theorem 2.3(i) of [Beran] p. 39. $)
    cmcm2 $p |- ( A C_H B <-> A C_H ( _|_ ` B ) ) $=
      ( cin cort cfv chj co wceq ccm wbr chincl choccl chjcom pjococ ineq2i
      opreq2i eqtr4 eqeq2i cmbr 3bitr4 ) AABEZABFGZEZHIZJAUEAUDFGZEZHIZJABKLAUD
      KLUFUIAUFUEUCHIUIUCUEABCDMAUDCBDNZMOUHUCUEHUGBABDPQRSTABCDUAAUDCUJUAUB $.
      $( [12-Aug-2004] $) $( [4-Nov-2000] $)

    $( Commutation with orthocomplement.  Remark in [Kalmbach] p. 23. $)
    cmcm3 $p |- ( A C_H B <-> ( _|_ ` A ) C_H B ) $=
      ( ccm wbr cort cfv cmcm2 cmcm choccl 3bitr4 ) BAEFBAGHZEFABEFMBEFBADCIABC
      DJMBACKDJL $.
      $( [12-Aug-2004] $) $( [4-Nov-2000] $)

    $( Commutation with orthocomplement.  Remark in [Kalmbach] p. 23. $)
    cmcm4 $p |- ( A C_H B <-> ( _|_ ` A ) C_H ( _|_ ` B ) ) $=
      ( ccm wbr cort cfv cmcm2 choccl cmcm3 bitr ) ABEFABGHZEFAGHMEFABCDIAMCBDJ
      KL $.
      $( [12-Aug-2004] $) $( [7-Aug-2004] $)

    $( Alternate definition of the commutes relation.  Remark in [Kalmbach]
       p. 23. $)
    cmbr2 $p |- ( A C_H B <-> A = ( ( A vH B ) i^i ( A vH ( _|_ ` B ) ) ) ) $=
      ( ccm wbr cort cfv cin chj co wceq cmcm4 choccl cmbr eqcom chjcl chincl
      chcon3 chdmm1 chdmj1 opreq12i eqtr eqeq2i 3bitrr 3bitr ) ABEFAGHZBGHZEFUG
      UGUHIZUGUHGHIZJKZLZAABJKZAUHJKZIZLZABCDMUGUHACNBDNZOUPUOALUGUOGHZLULAUOPU
      OAUMUNABCDQZAUHCUQQZRCSURUKUGURUMGHZUNGHZJKUKUMUNUSUTTVAUIVBUJJABCDUAAUHC
      UQUAUBUCUDUEUF $.
      $( [12-Aug-2004] $) $( [7-Aug-2004] $)

    ${
      cmcmi.1 $e |- A C_H B $.
      $( Commutation is symmetric.  Theorem 2(v) of [Kalmbach] p. 22. $)
      cmcmi $p |- B C_H A $=
        ( ccm wbr cmcm mpbi ) ABFGBAFGEABCDHI $.
        $( [12-Aug-2004] $) $( [7-Aug-2004] $)

      $( Commutation with orthocomplement.  Theorem 2.3(i) of [Beran] p. 39. $)
      cmcm2i $p |- A C_H ( _|_ ` B ) $=
        ( ccm wbr cort cfv cmcm2 mpbi ) ABFGABHIFGEABCDJK $.
        $( [12-Aug-2004] $) $( [7-Aug-2004] $)

      $( Commutation with orthocomplement.  Remark in [Kalmbach] p. 23. $)
      cmcm3i $p |- ( _|_ ` A ) C_H B $=
        ( ccm wbr cort cfv cmcm3 mpbi ) ABFGAHIBFGEABCDJK $.
        $( [13-Sep-2004] $) $( [7-Aug-2004] $)
    $}

    $( Alternate definition for the commutes relation.  Lemma 3 of [Kalmbach]
       p. 23. $)
    cmbr3 $p |- ( A C_H B <-> ( A i^i ( ( _|_ ` A ) vH B ) ) = ( A i^i B ) ) $=
      ( ccm wbr cort cfv chj co cin wceq cmcm cmbr2 bitr ineq2 inass chjcom
      ineq2i chabs2 eqtr choccl ineq12i eqtr3 syl6req sylbi chdmm3 incom
      eqeq1i opreq1 wss inss1 chincl pjoml2 ax-mp syl5eq cmbr sylibr impbi ) AB
      EFZAAGHZBIJZKZABKZLZUTBBAIJZBVAIJZKZLZVEUTBAEFVIABCDMBADCNOVIVDAVHKZVCBVH
      APAVFKZVGKVJVCAVFVGQVKAVGVBVKAABIJZKAVFVLABADCRSABCDTUABVADACUBRUCUDUEUFV
      EAVDABGHZKZIJZLUTVEVNGHZAKZVNIJZVOAVEVQVDLVRVOLVCVQVDAVPKVCVQVPVBAABCDUGS
      AVPUHUDUIVQVDVNIUJUFVNVQIJZAVRVNAUKVSALAVMULVNAAVMCBDUBUMZCUNUOVNVQVTVPAV
      NVTUBCUMRUDUPABCDUQURUS $.
      $( [12-Aug-2004] $) $( [6-Dec-2000] $)

    $( Alternate definition for the commutes relation. $)
    cmbr4 $p |- ( A C_H B <-> ( A i^i ( ( _|_ ` A ) vH B ) ) (_ B ) $=
      ( ccm wbr cort cfv chj co cin wceq wss cmbr3 inss2 sseq1 mpbiri wa inss1
      jctl ssin sylib choccl chub2 sslin ax-mp jctir eqss sylibr impbi bitr )
      ABEFAAGHZBIJZKZABKZLZUNBMZABCDNUPUQUPUQUOBMABOUNUOBPQUQUNUOMZUOUNMZRUPUQU
      RUSUQUNAMZUQRURUQUTAUMSTUNABUAUBBUMMUSBULDACUCUDBUMAUEUFUGUNUOUHUIUJUK $.
      $( [12-Aug-2004] $) $( [6-Dec-2000] $)

    $( Comparable Hilbert lattice elements commute.  Theorem 2.3(iii) of
       [Beran] p. 40. $)
    lecm $p |- ( A (_ B -> A C_H B ) $=
      ( wss cort cfv chj co cin ccm wbr ssinss1 cmbr4 sylibr ) ABEAAFGBHIZJBEAB
      KLAPBMABCDNO $.
      $( [22-Jan-2005] $) $( [16-Jan-2005] $)

    ${
      lecmi.1 $e |- A (_ B $.
      $( Comparable Hilbert lattice elements commute.  Theorem 2.3(iii) of
         [Beran] p. 40. $)
      lecmi $p |- A C_H B $=
        ( wss ccm wbr lecm ax-mp ) ABFABGHEABCDIJ $.
        $( [12-Aug-2004] $) $( [7-Aug-2004] $)
    $}

    $( A Hilbert lattice element commutes with its join. $)
    cmj1 $p |- A C_H ( A vH B ) $=
      ( chj co chjcl chub1 lecmi ) AABEFCABCDGABCDHI $.
      $( [12-Aug-2004] $) $( [7-Aug-2004] $)

    $( A Hilbert lattice element commutes with its join. $)
    cmj2 $p |- B C_H ( A vH B ) $=
      ( chj co chjcl chub2 lecmi ) BABEFDABCDGBADCHI $.
      $( [12-Aug-2004] $) $( [7-Aug-2004] $)

    $( A Hilbert lattice element commutes with its meet. $)
    cmm1 $p |- A C_H ( A i^i B ) $=
      ( cin chincl inss1 lecmi cmcmi ) ABEZAABCDFZCJAKCABGHI $.
      $( [13-Sep-2004] $) $( [7-Aug-2004] $)

    $( A Hilbert lattice element commutes with its meet. $)
    cmm2 $p |- B C_H ( A i^i B ) $=
      ( cin ccm cmm1 incom breqtr ) BBAEABEFBADCGBAHI $.
      $( [13-Sep-2004] $) $( [7-Aug-2004] $)
  $}

  $( Alternate definition for the commutes relation.  Lemma 3 of [Kalmbach]
     p. 23. $)
  cmbr3t $p |- ( ( A e. CH /\ B e. CH ) ->
              ( A C_H B <-> ( A i^i ( ( _|_ ` A ) vH B ) ) = ( A i^i B ) ) ) $=
    ( cch wcel ccm wbr cort cfv chj co cin wceq wb c0h cif breq1 id fveq2
    opreq1d ineq12d ineq1 eqeq12d bibi12d breq2 opreq2 ineq2d ineq2 h0elch
    elimel cmbr3 dedth2h ) ACDZBCDZABEFZAAGHZBIJZKZABKZLZMULANOZBEFZUTUTGHZBIJZ
    KZUTBKZLZMUTUMBNOZEFZUTVBVGIJZKZUTVGKZLZMABNNAUTLZUNVAUSVFAUTBEPVMUQVDURVEV
    MAUTUPVCVMQVMUOVBBIAUTGRSTAUTBUAUBUCBVGLZVAVHVFVLBVGUTEUDVNVDVJVEVKVNVCVIUT
    BVGVBIUEUFBVGUTUGUBUCUTVGANCUHUIBNCUHUIUJUK $.
    $( [15-Jun-2006] $) $( [14-Jun-2006] $)

  $( The zero Hilbert lattice element commutes with every element. $)
  cm0t $p |- ( A e. CH -> 0H C_H A ) $=
    ( cch wcel c0h ccm wbr cort cfv chj co cin wceq h0elch choccl chjclt mpan
    chm0t syl eqtr4d incom 3eqtr4g wb cmbr3t mpbird ) ABCZDAEFZDDGHZAIJZKZDAKZL
    ZUEUHDKZADKZUIUJUEULDUMUEUHBCZULDLUGBCUEUNDMNUGAOPUHQRAQSDUHTDATUADBCUEUFUK
    UBMDAUCPUD $.
    $( [18-Jun-2006] $) $( [16-Jun-2006] $)

  ${
    cmid.1 $e |- A e. CH $.
    $( The commutes relation is reflexive. $)
    cmid $p |- A C_H A $=
      ( ssid lecmi ) AABBACD $.
      $( [13-Sep-2004] $) $( [7-Aug-2004] $)
  $}

  $( Variation of orthomodular law.  Definition in [Kalmbach] p. 22. $)
  pjoml2t $p |- ( ( A e. CH /\ B e. CH /\ A (_ B ) ->
                ( A vH ( ( _|_ ` A ) i^i B ) ) = B ) $=
    ( cch wcel wss cort cfv cin chj co wceq wi c0h cif sseq1 id fveq2 ineq1d
    opreq12d eqeq1d imbi12d sseq2 ineq2 opreq2d eqeq12d h0elch elimel pjoml2
    dedth2h 3impia ) ACDZBCDZABEZAAFGZBHZIJZBKZUKULUMUQLUKAMNZBEZURURFGZBHZIJZB
    KZLURULBMNZEZURUTVDHZIJZVDKZLABMMAURKZUMUSUQVCAURBOVIUPVBBVIAURUOVAIVIPVIUN
    UTBAURFQRSTUABVDKZUSVEVCVHBVDURUBVJVBVGBVDVJVAVFURIBVDUTUCUDVJPUEUAURVDAMCU
    FUGBMCUFUGUHUIUJ $.
    $( [27-Jun-2006] $) $( [13-Jun-2006] $)

  $( Variation of orthomodular law. $)
  pjoml3t $p |- ( ( A e. CH /\ B e. CH ) ->
                ( B (_ A -> ( A i^i ( ( _|_ ` A ) vH B ) ) = B ) ) $=
    ( cch wcel wss cort cfv chj co cin wceq wi chil cif sseq2 id fveq2 opreq1d
    ineq12d eqeq1d imbi12d sseq1 opreq2 ineq2d eqeq12d helch elimel pjoml3
    dedth2h ) ACDZBCDZBAEZAAFGZBHIZJZBKZLBUJAMNZEZUQUQFGZBHIZJZBKZLUKBMNZUQEZUQ
    USVCHIZJZVCKZLABMMAUQKZULURUPVBAUQBOVHUOVABVHAUQUNUTVHPVHUMUSBHAUQFQRSTUABV
    CKZURVDVBVGBVCUQUBVIVAVFBVCVIUTVEUQBVCUSHUCUDVIPUEUAUQVCAMCUFUGBMCUFUGUHUI
    $.
    $( [13-Sep-2004] $) $( [24-Jun-2004] $)

  $( The orthomodular law.  Remark in [Kalmbach] p. 22. $)
  pjoml5t $p |- ( ( A e. CH /\ B e. CH ) ->
             ( A vH ( ( _|_ ` A ) i^i ( A vH B ) ) ) = ( A vH B ) ) $=
    ( cch wcel chj co wss cort cfv cin wceq wa pjoml2t pm3.26 chjclt chub1t
    syl3anc ) ACDZABEFZCDASGAAHISJEFSKRBCDZLASMRTNABOABPQ $.
    $( [30-Jun-2006] $) $( [12-Jun-2006] $)

  $( Commutation is symmetric.  Theorem 2(v) of [Kalmbach] p. 22. $)
  cmcmt $p |- ( ( A e. CH /\ B e. CH ) -> ( A C_H B <-> B C_H A ) ) $=
    ( cch wcel ccm wbr wb c0h cif wceq breq1 breq2 bibi12d h0elch elimel cmcm
    dedth2h ) ACDZBCDZABEFZBAEFZGRAHIZBEFZBUBEFZGUBSBHIZEFZUEUBEFZGABHHAUBJTUCU
    AUDAUBBEKAUBBELMBUEJUCUFUDUGBUEUBELBUEUBEKMUBUEAHCNOBHCNOPQ $.
    $( [14-Jun-2006] $) $( [13-Jun-2006] $)

  $( Commutation with orthocomplement.  Remark in [Kalmbach] p. 23. $)
  cmcm3t $p |- ( ( A e. CH /\ B e. CH ) ->
               ( A C_H B <-> ( _|_ ` A ) C_H B ) ) $=
    ( cch wcel ccm wbr cort cfv wb c0h cif wceq breq1 fveq2 breq1d bibi12d
    breq2 h0elch elimel cmcm3 dedth2h ) ACDZBCDZABEFZAGHZBEFZIUBAJKZBEFZUGGHZBE
    FZIUGUCBJKZEFZUIUKEFZIABJJAUGLZUDUHUFUJAUGBEMUNUEUIBEAUGGNOPBUKLUHULUJUMBUK
    UGEQBUKUIEQPUGUKAJCRSBJCRSTUA $.
    $( [14-Jun-2006] $) $( [13-Jun-2006] $)

  $( Commutation with orthocomplement.  Theorem 2.3(i) of [Beran] p. 39. $)
  cmcm2t $p |- ( ( A e. CH /\ B e. CH ) ->
               ( A C_H B <-> A C_H ( _|_ ` B ) ) ) $=
    ( cch wcel wa ccm wbr cort cfv wb cmcm3t ancoms cmcmt chocclt sylan2
    3bitr4d ) ACDZBCDZEBAFGZBHIZAFGZABFGATFGZRQSUAJBAKLABMQTCDUBUAJRATMBNOP $.
    $( [14-Jun-2006] $) $( [14-Jun-2006] $)

  $( Comparable Hilbert lattice elements commute.  Theorem 2.3(iii) of
     [Beran] p. 40. $)
  lecmt $p |- ( ( A e. CH /\ B e. CH /\ A (_ B ) -> A C_H B ) $=
    ( cch wcel wss ccm wbr wi c0h cif wceq sseq1 breq1 imbi12d sseq2 breq2
    h0elch elimel lecm dedth2h 3impia ) ACDZBCDZABEZABFGZUBUCUDUEHUBAIJZBEZUFBF
    GZHUFUCBIJZEZUFUIFGZHABIIAUFKUDUGUEUHAUFBLAUFBFMNBUIKUGUJUHUKBUIUFOBUIUFFPN
    UFUIAICQRBICQRSTUA $.
    $( [20-Jun-2006] $) $( [13-Jun-2006] $)

