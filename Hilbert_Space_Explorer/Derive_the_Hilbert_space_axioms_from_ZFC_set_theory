$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Derive the Hilbert space axioms from ZFC set theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y F $.  $d y U $.
    axhil.1 $e |- U = <. <. +h , .h >. , normh >. $.
    axhil.2 $e |- U e. CHil $.
    $( Derive axiom ~ ax-hilex from Hilbert space under ZF set theory.

       Before introducing the 18 axioms for Hilbert space, we first prove them
       as the conclusions of theorems ~ axhilex through ~ axhcompl , using ZFC
       set theory only.  These show that if we are given a known, fixed Hilbert
       space ` U = <. <. +h , .h >. , normh >. ` that satisfies their
       hypotheses, then we can derive the Hilbert space axioms as theorems of
       ZFC set theory.  In practice, in order to use these theorems to convert
       the Hilbert Space explorer to a ZFC-only subtheory, we would also
       have to provide definitions for the 3 (otherwise primitive) class
       constants ` +h ` , ` .h ` , and ` .ih ` before ~ df-hnorm above.  See
       also the comment in ~ ax-hilex . $)
    axhilex $p |- H~ e. V $=
      ( cva csm cop cno chil df-hba hlex ) DEFGFHIJ $.
      $( [23-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hfvadd from Hilbert space under ZF set theory. $)
    axhfvadd $p |- +h : ( H~ X. H~ ) --> H~ $=
      ( chl wcel chil cxp cva wf csm cop cno cba cfv df-hba fveq2i eqtr4i
      hlnvi h2hva hladdf ax-mp ) ADEFFGFHICAHFFHJKLKZMNAMNOAUBMBPQABACRSTUA $.
      $( [14-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hvcom from Hilbert space under ZF set theory. $)
    axhvcom $p |- ( ( A e. H~ /\ B e. H~ ) -> ( A +h B ) = ( B +h A ) ) $=
      ( chl wcel chil cva co wceq csm cop cno cba cfv df-hba fveq2i eqtr4i
      hlnvi h2hva hlcom mp3an1 ) CFGAHGBHGABIJBAIJKEABCIHHILMNMZOPCOPQCUDODRSCD
      CETUAUBUC $.
      $( [14-Jun-2008] $) $( [27-May-2008] $)

    $( Derive axiom  ~ ax-hvass from Hilbert space under ZF set theory. $)
    axhvass $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) -> ( ( A +h B ) +h C )
      = ( A +h ( B +h C ) ) ) $=
      ( chl wcel chil w3a cva co wceq csm cop cno cba cfv df-hba fveq2i eqtr4i
      hlnvi h2hva hlass mpan ) DGHAIHBIHCIHJABKLCKLABCKLKLMFABCDKIIKNOPOZQRDQRS
      DUFQETUADEDFUBUCUDUE $.
      $( [14-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hv0cl from Hilbert space under ZF set theory. $)
    axhv0cl $p |- 0h e. H~ $=
      ( chl wcel c0v chil cva csm cop cno cba cfv df-hba fveq2i eqtr4i cn0v
      df-h0v hl0cl ax-mp ) ADEFGECAGFGHIJKJZLMALMNAUALBOPFUAQMAQMRAUAQBOPST $.
      $( [14-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hvaddid from Hilbert space under ZF set theory. $)
    axhvaddid $p |- ( A e. H~ -> ( A +h 0h ) = A ) $=
      ( chl wcel chil c0v cva co wceq csm cop cno cba cfv df-hba fveq2i eqtr4i
      hlnvi h2hva cn0v df-h0v hladdid mpan ) BEFAGFAHIJAKDABIGHGILMNMZOPBOPQBUF
      OCRSBCBDTUAHUFUBPBUBPUCBUFUBCRSUDUE $.
      $( [14-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hfvmul from Hilbert space under ZF set theory. $)
    axhfvmul $p |- .h : ( CC X. H~ ) --> H~ $=
      ( chl wcel cc chil cxp csm wf cva cop cno cba cfv df-hba fveq2i eqtr4i
      hlnvi h2hsm hlmulf ax-mp ) ADEFGHGIJCIAGGKILMLZNOANOPAUCNBQRABACSTUAUB $.
      $( [14-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hvmulid from Hilbert space under ZF set theory. $)
    axhvmulid $p |- ( A e. H~ -> ( 1 .h A ) = A ) $=
      ( chl wcel chil c1 csm co wceq cva cop cno cba cfv df-hba fveq2i eqtr4i
      hlnvi h2hsm hlmulid mpan ) BEFAGFHAIJAKDAIBGGLIMNMZOPBOPQBUDOCRSBCBDTUAUB
      UC $.
      $( [14-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hvmulass from Hilbert space under ZF set theory. $)
    axhvmulass $p |- ( ( A e. CC /\ B e. CC /\ C e. H~ )
      -> ( ( A x. B ) .h C ) = ( A .h ( B .h C ) ) ) $=
      ( chl wcel cc chil w3a cmul co csm wceq cva cop cno cba cfv df-hba
      fveq2i eqtr4i hlnvi h2hsm hlmulass mpan ) DGHAIHBIHCJHKABLMCNMABCNMNMOFAB
      CNDJJPNQRQZSTDSTUADUHSEUBUCDEDFUDUEUFUG $.
      $( [14-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hvdistr1 from Hilbert space under ZF set theory. $)
    axhvdistr1 $p |- ( ( A e. CC /\ B e. H~ /\ C e. H~ )
      -> ( A .h ( B +h C ) ) = ( ( A .h B ) +h ( A .h C ) ) ) $=
      ( chl wcel cc chil w3a cva co csm wceq cop cno cba cfv df-hba fveq2i
      eqtr4i hlnvi h2hva h2hsm hldi mpan ) DGHAIHBJHCJHKABCLMNMABNMACNMLMOFABCN
      DLJJLNPQPZRSDRSTDUHREUAUBDEDFUCZUDDEUIUEUFUG $.
      $( [17-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hvdistr2 from Hilbert space under ZF set theory. $)
    axhvdistr2 $p |- ( ( A e. CC /\ B e. CC /\ C e. H~ )
      -> ( ( A + B ) .h C ) = ( ( A .h C ) +h ( B .h C ) ) ) $=
      ( chl wcel cc chil w3a caddc co csm cva wceq cop cno cba cfv df-hba
      fveq2i eqtr4i hlnvi h2hva h2hsm hldir mpan ) DGHAIHBIHCJHKABLMCNMACNMBCNM
      OMPFABCNDOJJONQRQZSTDSTUADUISEUBUCDEDFUDZUEDEUJUFUGUH $.
      $( [19-Jun-2008] $) $( [31-May-2008] $)

    $( Derive axiom  ~ ax-hvmul0 from Hilbert space under ZF set theory. $)
    axhvmul0 $p |- ( A e. H~ -> ( 0 .h A ) = 0h ) $=
      ( chl wcel chil cc0 csm co c0v wceq cva cop cno cba cfv df-hba fveq2i
      eqtr4i hlnvi h2hsm cn0v df-h0v hlmul0 mpan ) BEFAGFHAIJKLDAIBGKGMINONZPQB
      PQRBUGPCSTBCBDUAUBKUGUCQBUCQUDBUGUCCSTUEUF $.
      $( [19-Jun-2008] $) $( [31-May-2008] $)

    ${
      axhfi.1 $e |- .ih = ( .i ` U ) $.
      $( Derive axiom  ~ ax-hfi from Hilbert space under ZF set theory. $)
      axhfi $p |- .ih : ( H~ X. H~ ) --> CC $=
        ( chl wcel chil cxp cc csp wf cva csm cop cno cba cfv df-hba fveq2i
        eqtr4i hlipf ax-mp ) AEFGGHIJKCJAGGLMNONZPQAPQRAUCPBSTDUAUB $.
        $( [21-Jun-2008] $) $( [31-May-2008] $)

      $( Derive axiom  ~ ax-his1 from Hilbert space under ZF set theory. $)
      axhis1 $p |- ( ( A e. H~ /\ B e. H~ )
        -> ( A .ih B ) = ( * ` ( B .ih A ) ) ) $=
        ( chl wcel chil csp co ccj cfv wceq cva csm cop cno cba df-hba fveq2i
        eqtr4i hlipcj mp3an1 ) CGHAIHBIHABJKBAJKLMNEABJCIIOPQRQZSMCSMTCUESDUAUB
        FUCUD $.
        $( [21-Jun-2008] $) $( [31-May-2008] $)

      $( Derive axiom  ~ ax-his2 from Hilbert space under ZF set theory. $)
      axhis2 $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ )
        -> ( ( A +h B ) .ih C ) = ( ( A .ih C ) + ( B .ih C ) ) ) $=
        ( chl wcel chil w3a cva co csp caddc wceq csm cop cno cba cfv df-hba
        fveq2i eqtr4i hlnvi h2hva hlipdir mpan ) DHIAJIBJICJIKABLMCNMACNMBCNMOM
        PFABCNDLJJLQRSRZTUADTUAUBDUITEUCUDDEDFUEUFGUGUH $.
        $( [21-Jun-2008] $) $( [31-May-2008] $)

      $( Derive axiom  ~ ax-his3 from Hilbert space under ZF set theory. $)
      axhis3 $p |- ( ( A e. CC /\ B e. H~ /\ C e. H~ )
        -> ( ( A .h B ) .ih C ) = ( A x. ( B .ih C ) ) ) $=
        ( chl wcel cc chil w3a csm co csp cmul wceq cva cop cno cba cfv df-hba
        fveq2i eqtr4i hlnvi h2hsm hlipass mpan ) DHIAJIBKICKILABMNCONABCONPNQFA
        BCOMDKKRMSTSZUAUBDUAUBUCDUJUAEUDUEDEDFUFUGGUHUI $.
        $( [21-Jun-2008] $) $( [31-May-2008] $)

      $( Derive axiom  ~ ax-his4 from Hilbert space under ZF set theory. $)
      axhis4 $p |- ( ( A e. H~ /\ A =/= 0h ) -> 0 < ( A .ih A ) ) $=
        ( chl wcel chil c0v wne cc0 csp co clt wbr cva csm cop cno cba cfv
        df-hba fveq2i eqtr4i cn0v df-h0v hlipgt0 mp3an1 ) BFGAHGAIJKAALMNODALBH
        IHPQRSRZTUABTUAUBBUITCUCUDIUIUEUABUEUAUFBUIUECUCUDEUGUH $.
        $( [22-Jun-2008] $) $( [31-May-2008] $)
    $}

    $( Derive axiom  ~ ax-hcompl from Hilbert space under ZF set theory. $)
    axhcompl $p |- ( F e. Cauchy -> E. x e. H~ F ~~>v x ) $=
      ( vy ccau wcel cv chli wbr chil wrex cims cfv cca cn cm co wa clm chl
      cva csm cop cno cba df-hba fveq2i eqtr4i eqid hlcompl mpan anim1i cin
      hlnvi h2hcau eleq2i elin bitri cres h2hlm breqi visset brres rexbii
      r19.41v 3imtr4i breq2 cbvrexv sylib ) CGHZCFIZJKZFLMZCAIZJKZALMCBNOZPOZHZ
      CLQRSZHZTZCVMVRUAOZKZFLMZWBTZVLVOVTWFWBBUBHVTWFEFVRBCLLUCUDUEUFUEZUGOBUGO
      UHBWHUGDUIUJZVRUKZULUMUNVLCVSWAUOZHWCGWKCVRBDBEUPZWIWJUQURCVSWAUSUTVOWEWB
      TZFLMWGVNWMFLVNCVMWDWAVAZKWMCVMJWNVRBDWLWIWJVBVCCVMWDWAFVDVEUTVFWEWBFLVGU
      TVHVNVQFALVMVPCJVIVJVK $.
      $( [22-Jun-2008] $) $( [6-Jun-2008] $)
  $}

