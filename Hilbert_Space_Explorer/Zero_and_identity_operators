$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Zero and identity operators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the Hilbert space zero operator.  See ~ df0op2 for alternate
     definition. $)
  df-h0op $a |- 0hop = ( proj ` 0H ) $.

  $( Define the Hilbert space identity operator.  See ~ dfiop2 for alternate
     definition. $)
  df-iop $a |- Iop = ( proj ` ~H ) $.

  $( Value of the zero Hilbert space operator (null projector).  Remark in
     [Beran] p. 111. $)
  ho0val $p |- ( A e. ~H -> ( 0hop ` A ) = 0h ) $=
    ( chil wcel ch0o cfv cpj cmv co c0v cort choc1 fveq2i df-h0op eqtr4i fveq1i
    c0h cch wceq helch pjo mpan syl5eqr pjhcli hvsubid syl eqtrd ) ABCZADEZABFE
    EZUIGHZIUGUHABJEZFEZEZUJAULDULPFEDUKPFKLMNOBQCUGUMUJRSABTUAUBUGUIBCUJIRABSU
    CUIUDUEUF $.
    $( [7-Feb-2006] $)

  $( Functionality of the zero Hilbert space operator. $)
  ho0f $p |- 0hop : ~H --> ~H $=
    ( chil ch0o wf c0h cpj cfv h0elch pjfi df-h0op feq1i mpbir ) AABCAADEFZCDGH
    AABLIJK $.
    $( [10-Mar-2006] $)

  $( Alternate definition of Hilbert space zero operator. $)
  df0op2 $p |- 0hop = ( ~H X. 0H ) $=
    ( vx ch0o chil c0v csn cxp c0h wf wceq wfn cfv wral fconstfv ho0f ffn ax-mp
    cv ho0val rgen mpbir2an ax-hv0cl elisseti fconst2 mpbi df-ch0 xpeq2i eqtr4i
    ) BCDEZFZCGFCUHBHZBUIIUJBCJZAQZBKDIZACLACDBMCCBHUKNCCBOPUMACULRSTCDBDCUAUBU
    CUDGUHCUEUFUG $.
    $( [7-Aug-2006] $)

  $( Alternate definition of Hilbert space identity operator. $)
  dfiop2 $p |- Iop = ( _I |` ~H ) $=
    ( vx chio chil cpj cfv cid cres df-iop wceq helch pjfni fnresi wa wral wcel
    wfn cv pjch1 fvresi eqtr4d rgen eqfnfv2 mpbiri mp2an eqtri ) BCDEZFCGZHUFCP
    ZUGCPZUFUGIZCJKCLUHUIMUJAQZUFEZUKUGEZIZACNUNACUKCOULUKUMUKRCUKSTUAACUFUGUBU
    CUDUE $.
    $( [7-Aug-2006] $)

  $( Functionality of the Hilbert space identity operator. $)
  hoif $p |- Iop : ~H -1-1-onto-> ~H $=
    ( chil chio wf1o cid cres f1oi wceq wb dfiop2 f1oeq1 ax-mp mpbir ) AABCZAAD
    AEZCZAFBNGMOHIAABNJKL $.
    $( [8-Aug-2006] $)

  $( The value of the Hilbert space identity operator. $)
  hoival $p |- ( A e. ~H -> ( Iop ` A ) = A ) $=
    ( chil wcel chio cfv cpj df-iop fveq1i pjch1 syl5eq ) ABCADEABFEZEAADKGHAIJ
    $.
    $( [8-Aug-2006] $)

  $( Composition with the Hilbert space identity operator. $)
  hoico1 $p |- ( T : ~H --> ~H -> ( T o. Iop ) = T ) $=
    ( chil wf chio ccom cid cres dfiop2 coeq2i fcoi1 syl5eq ) BBACADEAFBGZEADLA
    HIBBAJK $.
    $( [24-Aug-2006] $)

  $( Composition with the Hilbert space identity operator. $)
  hoico2 $p |- ( T : ~H --> ~H -> ( Iop o. T ) = T ) $=
    ( chil wf chio ccom cid cres dfiop2 coeq1i fcoi2 syl5eq ) BBACDAEFBGZAEADLA
    HIBBAJK $.
    $( [24-Aug-2006] $)


