$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Zero and identity operators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the Hilbert space zero operator.  See ~ df0op2 for alternate
     definition. $)
  df-0op $a |- 0op = ( proj ` 0H ) $.

  $( Define the Hilbert space identity operator.  See ~ dfiop2 for alternate
     definition. $)
  df-iop $a |- Iop = ( proj ` H~ ) $.

  $( Alternate definition of Hilbert space zero operator. $)
  df0op2 $p |- 0op = ( H~ X. 0H ) $=
    ( vx c0h cpj cfv chil c0v csn cxp ch0o wf wceq wfn cv wral fconstfv h0elch
    pjfn pjch0t rgen mpbir2an ax-hv0cl elisseti fconst2 mpbi df-0op df-ch0
    xpeq2 ax-mp 3eqtr4 ) BCDZEFGZHZIEBHZEUKUJJZUJULKUNUJELAMZUJDFKZAENAEFUJOBPQ
    UPAEUORSTEFUJFEUAUBUCUDUEBUKKUMULKUFBUKEUGUHUI $.
    $( [7-Aug-2006] $) $( [7-Aug-2006] $)

  $( Alternate definition of Hilbert space identity operator. $)
  dfiop2 $p  |- Iop = ( I |` H~ ) $=
    ( vx chio chil cpj cfv cid cres df-iop wfn wceq helch pjfn fnresi wa cv
    wral eqid wcel pjch1t fvresi eqtr4d rgen pm3.2i eqfnfv mpbiri mp2an eqtr )
    BCDEZFCGZHUHCIZUICIZUHUIJZCKLCMUJUKNULCCJZAOZUHEZUNUIEZJZACPZNUMURCQUQACUNC
    RUOUNUPUNSCUNTUAUBUCACCUHUIUDUEUFUG $.
    $( [7-Aug-2006] $) $( [7-Aug-2006] $)

  $( Functionality of the zero Hilbert space operator. $)
  ho0f $p |- 0op : H~ --> H~ $=
    ( chil ch0o wf c0h cpj cfv h0elch pjf wceq wb df-0op feq1 ax-mp mpbir ) AAB
    CZAADEFZCZDGHBPIOQJKAABPLMN $.
    $( [7-Aug-2006] $) $( [10-Mar-2006] $)

  $( The functionality of the zero Hilbert space operator. $)
  ho0fOLD $p |- ( H~ X. 0H ) : H~ --> H~ $=
    ( chil c0h cxp wf c0v csn wss ax-hv0cl elisseti fconst wcel snssi ax-mp
    fss mp2an wceq wb df-ch0 xpeq2 feq1 syl mpbir ) AAABCZDZAAAEFZCZDZAUEUFDUEA
    GZUGAEEAHIJEAKUHHEALMAUEAUFNOBUEPZUDUGQZRUIUCUFPUJBUEASAAUCUFTUAMUB $.
    $( [18-Mar-2006] $) $( [10-Mar-2006] $)

  $( Functionality of the Hilbert space identity operator. $)
  hoif $p |- Iop : H~ -1-1-onto-> H~ $=
    ( chil chio wf1o cid cres f1oi wceq wb dfiop2 f1oeq1 ax-mp mpbir ) AABCZAAD
    AEZCZAFBNGMOHIAABNJKL $.
    $( [9-Aug-2006] $) $( [8-Aug-2006] $)

  $( Zero element for Hilbert space operators.  Obsolete; use ~ df0op2
     instead. $)
  ho0OLD $p  |- ( proj ` 0H ) = ( H~ X. 0H ) $=
    ( vx c0h cpj cfv chil c0v csn cxp wf wceq wfn cv wral fconstfv h0elch pjfn
    pjch0t rgen mpbir2an ax-hv0cl elisseti fconst2 mpbi df-ch0 xpeq2 ax-mp
    eqtr4 ) BCDZEFGZHZEBHZEUIUHIZUHUJJULUHEKALZUHDFJZAEMAEFUHNBOPUNAEUMQRSEFUHF
    ETUAUBUCBUIJUKUJJUDBUIEUEUFUG $.
    $( [21-Sep-2004] $) $( [27-Aug-2004] $)

  $( The value of the zero Hilbert space operator. $)
  ho0valt $p |- ( A e. H~ -> ( 0op ` A ) = 0h ) $=
    ( chil wcel c0h cpj cfv c0v ch0o pjch0t df-0op fveq1i syl5eq ) ABCADEFZFGAH
    FAIAHMJKL $.
    $( [8-Aug-2006] $) $( [7-Feb-2006] $)

  $( The value of the identically zero Hilbert space operator. $)
  ho0valtOLD $p |- ( A e. H~ -> ( ( H~ X. 0H ) ` A ) = 0h ) $=
    ( chil wcel c0v csn cxp cfv c0h ax-hv0cl elisseti fvconst2 wceq df-ch0
    xpeq2 ax-mp fveq1i syl5eq ) ABCABDEZFZGDABHFZGBDADBIJKATSHRLTSLMHRBNOPQ $.
    $( [11-Feb-2006] $) $( [7-Feb-2006] $)

  $( The value of the Hilbert space identity operator. $)
  hoivalt $p |- ( A e. H~ -> ( Iop ` A ) = A ) $=
    ( chil wcel cpj cfv chio pjch1t df-iop fveq1i syl5eq ) ABCABDEZEAAFEAGAFKHI
    J $.
    $( [9-Aug-2006] $) $( [8-Aug-2006] $)

  $( Composition with the Hilbert space identity operator. $)
  hoico1t $p |- ( T : H~ --> H~ -> ( T o. Iop ) = T ) $=
    ( chil wf cid cres ccom chio fcoi1 dfiop2 coeq2i syl5eq ) BBACADBEZFAAGFBBA
    HGLAIJK $.
    $( [24-Aug-2006] $) $( [24-Aug-2006] $)

  $( Composition with the Hilbert space identity operator. $)
  hoico2t $p |- ( T : H~ --> H~ -> ( Iop o. T ) = T ) $=
    ( chil wf cid cres ccom chio fcoi2 dfiop2 coeq1i syl5eq ) BBACDBEZAFAGAFBBA
    HGLAIJK $.
    $( [25-Aug-2006] $) $( [24-Aug-2006] $)


