$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Norms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Alternate definition of the norm of a vector of Hilbert space.
       Definition of norm in [Beran] p. 96. $)
    dfhnorm2 $p |- normh =
                  { <. x , y >. | ( x e. ~H /\ y = ( sqr ` ( x .ih x ) ) ) } $=
      ( cno cv csp cdm wcel co csqr cfv wceq wa copab chil df-hnorm cxp cc
      ax-hfi fdmi dmeqi dmxpid eqtr2i eleq2i anbi1i opabbii eqtr4i ) CADZEFZFZG
      ZBDUGUGEHIJKZLZABMUGNGZUKLZABMABOUNULABUMUJUKNUIUGUINNPZFNUHUOUOQERSTNUAU
      BUCUDUEUF $.
      $( [12-Jun-2008] $) $( [6-Jun-2008] $)

    $( The norm function maps from Hilbert space to reals. $)
    normf $p |- normh : ~H --> RR $=
      ( vx vy chil cr cv csp co csqr cfv cno dfhnorm2 wcel cc0 cle wbr sqrcl
      hiidrcl hiidge0 sylanc fopab ) ABCDAEZUAFGZHIZJABKUBDLMUBNOUCDLUACLUBPUAQ
      UARST $.
      $( [6-Sep-2007] $) $( [6-Sep-2007] $)
  $}

  ${
    $d x y A $.
    $( The value of the norm of a vector in Hilbert space.  Definition of norm
       in [Beran] p. 96.  In the literature, the norm of ` A ` is usually
       written as "|| ` A ` ||", but we use function value notation to take
       advantage of our existing theorems about functions. $)
    normval $p |- ( A e. ~H -> ( normh ` A ) = ( sqr ` ( A .ih A ) ) ) $=
      ( vx vy cv csp co csqr cfv chil cno wceq opreq12 anidms fveq2d dfhnorm2
      fvex fvopab4 ) BCABDZREFZGHAAEFZGHIJRAKZSTGUASTKRARAELMNBCOTGPQ $.
      $( [29-May-1999] $)
  $}

  $( Real closure of the norm of a vector. $)
  normcl $p |- ( A e. ~H -> ( normh ` A ) e. RR ) $=
    ( chil cr cno normf ffvelrni ) BCADEF $.
    $( [29-May-1999] $)

  $( The norm of a vector is nonnegative. $)
  normge0 $p |- ( A e. ~H -> 0 <_ ( normh ` A ) ) $=
    ( chil wcel cc0 csp co csqr cfv cno cle cr wbr sqrge0 hiidrcl hiidge0
    sylanc normval breqtrrd ) ABCZDAAEFZGHZAIHJTKCDTJLDUAJLSTMANAOPAQR $.
    $( [29-May-1999] $)

  $( The norm of nonzero vector is positive. $)
  normgt0OLD $p |- ( A e. ~H -> ( -. A = 0h <-> 0 < ( normh ` A ) ) ) $=
    ( chil wcel c0v wceq wn cc0 csp co csqr cfv clt wbr cno cr wa sqrgt0
    hiidrcl adantr wne ax-his4 df-ne sylan2br sylanc ex opreq1 hi01 sylan9eqr
    fveq2d sqr0 syl6eq wb cle sqrcl hiidge0 0re jctir lttri3 syl pm3.27 syl6bi
    syld con2d impbid normval breq2d bitr4d ) ABCZADEZFZGAAHIZJKZLMZGANKZLMVHVJ
    VMVHVJVMVKOCZGVKLMZVMVHVJPVKQVHVOVJARZSVHADTVPVJAUAADUBUCUDUEVHVIVMVHVIVLGE
    ZVMFZVHVIVRVHVIPZVLGJKGVTVKGJVIVHVKDAHIGADAHUFAUGUHUIUJUKUEVHVRVLGLMFZVSPZV
    SVHVLOCZGOCZPVRWBULVHWCWDVOGVKUMMWCVHVKUNVQAUOUDUPUQVLGURUSWAVSUTVAVBVCVDVH
    VNVLGLAVEVFVG $.
    $( [29-May-1999] $)

  $( The norm of nonzero vector is positive. $)
  normgt0 $p |- ( A e. ~H -> ( A =/= 0h <-> 0 < ( normh ` A ) ) ) $=
    ( chil wcel c0v wne cc0 csp co csqr cfv clt wbr cno cr wa sqrgt0 hiidrcl
    adantr ax-his4 sylanc ex wceq wn opreq1 hi01 sylan9eqr fveq2d sqr0 syl6eq
    wb cle sqrcl hiidge0 0re jctir lttri3 syl pm3.27 syl6bi syld necon2ad
    impbid normval breq2d bitr4d ) ABCZADEZFAAGHZIJZKLZFAMJZKLVFVGVJVFVGVJVHNCZ
    FVHKLVJVFVGOVHPVFVLVGAQZRASTUAVFVJADVFADUBZVIFUBZVJUCZVFVNVOVFVNOZVIFIJFVQV
    HFIVNVFVHDAGHFADAGUDAUEUFUGUHUIUAVFVOVIFKLUCZVPOZVPVFVINCZFNCZOVOVSUJVFVTWA
    VLFVHUKLVTVFVHULVMAUMTUNUOVIFUPUQVRVPURUSUTVAVBVFVKVIFKAVCVDVE $.
    $( [3-May-2006] $) $( [10-Apr-2006] $)

  $( The norm of a zero vector. $)
  norm0 $p |- ( normh ` 0h ) = 0 $=
    ( c0v cno cfv csp co csqr cc0 chil wcel wceq ax-hv0cl normval ax-mp hi01
    fveq2d sqr0 3eqtri ) ABCZAADEZFCZGFCZGAHIZRTJKALMUBTUAJKUBSGFANOMPQ $.
    $( [30-May-1999] $)

  $( Theorem 3.3(i) of [Beran] p. 97. $)
  norm-i $p |- ( A e. ~H -> ( ( normh ` A ) = 0 <-> A = 0h ) ) $=
    ( chil wcel c0v wceq cno cfv cc0 wne clt wbr normgt0 cr cle wb 0re leltne
    mp3an1 normcl normge0 sylanc bitrd necon4bid bicomd ) ABCZADEAFGZHEUEADUFHU
    EADIHUFJKZUFHIZALUFMCZHUFNKZUGUHOZUEHMCUIUJUKPHUFQRASATUAUBUCUD $.
    $( [29-Jul-1999] $)

  $( A norm is nonzero iff its argument is a nonzero vector. $)
  normne0 $p |- ( A e. ~H -> ( ( normh ` A ) =/= 0 <-> A =/= 0h ) ) $=
    ( chil wcel cno cfv cc0 c0v norm-i necon3bid ) ABCADEFAGAHI $.
    $( [12-Mar-2006] $) $( [11-Mar-2006] $)

  ${
    normcl.1 $e |- A e. ~H $.
    $( Real closure of the norm of a vector. $)
    normcli $p |- ( normh ` A ) e. RR $=
      ( chil wcel cno cfv cr normcl ax-mp ) ACDAEFGDBAHI $.
      $( [30-Sep-1999] $)

    $( The square of a norm. $)
    normsqi $p |- ( ( normh ` A ) ^ 2 ) = ( A .ih A ) $=
      ( cno cfv c2 cexp co csp csqr chil wcel wceq normval ax-mp opreq1i cc0
      cle wbr hiidge0 cr hiidrcl sqsqri eqtri ) ACDZEFGAAHGZIDZEFGZUEUDUFEFAJKZ
      UDUFLBAMNOPUEQRZUGUELUHUIBASNUEUHUETKBAUANUBNUC $.
      $( [21-Aug-1999] $)

    $( Theorem 3.3(i) of [Beran] p. 97. $)
    norm-i.i $p |- ( ( normh ` A ) = 0 <-> A = 0h ) $=
      ( chil wcel cno cfv cc0 wceq c0v wb norm-i ax-mp ) ACDAEFGHAIHJBAKL $.
      $( [5-Sep-1999] $)
  $}

  $( The square of a norm. $)
  normsq $p |- ( A e. ~H -> ( ( normh ` A ) ^ 2 ) = ( A .ih A ) ) $=
    ( chil wcel cno cfv c2 cexp co csp wceq c0v cif fveq2 opreq1d id opreq12d
    eqeq12d ax-hv0cl elimel normsqi dedth ) ABCZADEZFGHZAAIHZJUBAKLZDEZFGHZUFUF
    IHZJAKAUFJZUDUHUEUIUJUCUGFGAUFDMNUJAUFAUFIUJOZUKPQUFAKBRSTUA $.
    $( [3-Jul-2005] $) $( [12-May-2005] $)

  ${
    normsub0.1 $e |- A e. ~H $.
    normsub0.2 $e |- B e. ~H $.
    $( Two vectors are equal iff the norm of their difference is zero. $)
    normsub0i $p |- ( ( normh ` ( A -h B ) ) = 0 <-> A = B ) $=
      ( cmv co cno cfv cc0 wceq c0v hvsubcli norm-i.i hvsubeq0i bitri ) ABEFZGH
      IJPKJABJPABCDLMABCDNO $.
      $( [18-Aug-1999] $)
  $}

  $( Two vectors are equal iff the norm of their difference is zero. $)
  normsub0 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( normh ` ( A -h B ) ) = 0 <-> A = B ) ) $=
    ( chil wcel cmv co cno cfv cc0 wceq wb c0v cif opreq1 fveq2d eqeq1d eqeq1
    bibi12d opreq2 eqeq2 ax-hv0cl elimel normsub0i dedth2h ) ACDZBCDZABEFZGHZIJ
    ZABJZKUEALMZBEFZGHZIJZUKBJZKUKUFBLMZEFZGHZIJZUKUPJZKABLLAUKJZUIUNUJUOVAUHUM
    IVAUGULGAUKBENOPAUKBQRBUPJZUNUSUOUTVBUMURIVBULUQGBUPUKESOPBUPUKTRUKUPALCUAU
    BBLCUAUBUCUD $.
    $( [18-Aug-1999] $)

  ${
    norm-ii.1 $e |- A e. ~H $.
    norm-ii.2 $e |- B e. ~H $.
    $( Triangle inequality for norms.  Theorem 3.3(ii) of [Beran] p. 97. $)
    norm-ii.i $p |- ( normh ` ( A +h B ) ) <_
                  ( ( normh ` A ) + ( normh ` B ) ) $=
      ( cva co csp csqr cfv caddc cno cle c2 cexp wbr cmul c1 ccj cr wcel wceq
      1re ax1cn cjrebi mpbi opreq1i hicli mulid2i eqtri opreq12i cc0 cabs 0re
      lt01 ltleii absidi ax-mp normlem7 eqbrtrri cneg eqid normlem2 cjcli
      mulcli addcli negrebi eqeltrri 2re chil hiidge0 hiidrcl sqrcli remulcli
      readdcli leadd2i normlem8 addcomi opreq2i recni binom2i sqcli 2cn add23i
      sqsqri 3eqtri 3brtr4i wb hvaddcli sqge0i resqcli sqrlei mp2an sqrge0i
      addge0i sqrsqi breqtri normval ) ABEFZWRGFZHIZAAGFZHIZBBGFZHIZJFZWRKIZAKI
      ZBKIZJFLWTXEMNFZHIZXELWSXILOZWTXJLOZXAXCJFZBAGFZABGFZJFZJFZXMMXBXDPFZPFZJ
      FZWSXILXPXSLOXQXTLOQRIZXNPFZQXOPFZJFZXPXSLYBXNYCXOJYBQXNPFXNYAQXNPQSTYAQU
      AUBQUCUDUEUFXNBADCUGZUHUIXOABCDUGZUHUJZQBAUCDCUKQLOQULIQUAUKQUMUBUNUOQUBU
      PUQURUSXPXSXMYDXPSYGYDUTZSTYDSTYHQBAUCDCYHVAVBYDYBYCYAXNQUCVCYEVDQXOUCYFV
      DVEVFUEVGMXRVHXBXDUKXALOZXBSTAVITZYICAVJUQZXAYJXASTCAVKUQZVLUQZUKXCLOZXDS
      TBVITZYNDBVJUQZXCYOXCSTDBVKUQZVLUQZVMVMXAXCYLYQVNVOUEWSXMXOXNJFZJFXQABABC
      DCDVPYSXPXMJXOXNYFYEVQVRUIXIXBMNFZXSJFXDMNFZJFYTUUAJFZXSJFXTXBXDXBYMVSZXD
      YRVSZVTYTXSUUAXBUUCWAMXRWBXBXDUUCUUDVDVDXDUUDWAWCUUBXMXSJYTXAUUAXCJYIYTXA
      UAYKXAYLWDUQYNUUAXCUAYPXCYQWDUQUJUFWEWFUKWSLOZUKXILOXKXLWGWRVITZUUEABCDWH
      ZWRVJUQXEXBXDYMYRVNZWIWSXIUUFWSSTUUGWRVKUQXEUUHWJWKWLUEUKXELOZXJXEUAUKXBL
      OZUKXDLOZUUIYIUUJYKXAYLWMUQYNUUKYPXCYQWMUQXBXDYMYRWNWLXEUUHWOUQWPUUFXFWTU
      AUUGWRWQUQXGXBXHXDJYJXGXBUACAWQUQYOXHXDUADBWQUQUJWF $.
      $( [11-Aug-1999] $)
  $}

  $( Triangle inequality for norms.  Theorem 3.3(ii) of [Beran] p. 97. $)
  norm-ii $p |- ( ( A e. ~H /\ B e. ~H ) ->
               ( normh ` ( A +h B ) ) <_ ( ( normh ` A ) + ( normh ` B ) ) ) $=
    ( chil wcel cva co cno cfv caddc cle wbr c0v cif wceq opreq1 fveq2d fveq2
    opreq1d breq12d opreq2 opreq2d ax-hv0cl elimel norm-ii.i dedth2h ) ACDZBCDZ
    ABEFZGHZAGHZBGHZIFZJKUFALMZBEFZGHZUMGHZUKIFZJKUMUGBLMZEFZGHZUPURGHZIFZJKABL
    LAUMNZUIUOULUQJVCUHUNGAUMBEOPVCUJUPUKIAUMGQRSBURNZUOUTUQVBJVDUNUSGBURUMETPV
    DUKVAUPIBURGQUASUMURALCUBUCBLCUBUCUDUE $.
    $( [11-Mar-2006] $) $( [10-Mar-2006] $)

  ${
    norm-iii.1 $e |- A e. CC $.
    norm-iii.2 $e |- B e. ~H $.
    $( Theorem 3.3(iii) of [Beran] p. 97. $)
    norm-iii.i $p |- ( normh ` ( A .h B ) )
         = ( ( abs ` A ) x. ( normh ` B ) ) $=
      ( csm co csp csqr cfv ccj cmul cno cabs his35i fveq2i cjmulrcli chil
      wcel cr hiidrcl ax-mp cjmulge0i cc0 cle wbr hiidge0 sqrmulii eqtri wceq
      hvmulcli normval cc absval opreq12i 3eqtr4i ) ABEFZUPGFZHIZAAJIKFZHIZBBGF
      ZHIZKFZUPLIZAMIZBLIZKFURUSVAKFZHIVCUQVGHAABBCCDDNOUSVAACPBQRZVASRDBTUAACU
      BVHUCVAUDUEDBUFUAUGUHUPQRVDURUIABCDUJUPUKUAVEUTVFVBKAULRVEUTUICAUMUAVHVFV
      BUIDBUKUAUNUO $.
      $( [29-Jul-1999] $)
  $}

  $( Theorem 3.3(iii) of [Beran] p. 97. $)
  norm-iii $p |- ( ( A e. CC /\ B e. ~H )
             -> ( normh ` ( A .h B ) ) = ( ( abs ` A ) x. ( normh ` B ) ) ) $=
    ( cc wcel chil csm co cno cfv cabs cmul wceq cc0 cif c0v opreq1 fveq2d
    fveq2 opreq1d eqeq12d opreq2 opreq2d 0cn elimel ax-hv0cl norm-iii.i
    dedth2h ) ACDZBEDZABFGZHIZAJIZBHIZKGZLUHAMNZBFGZHIZUOJIZUMKGZLUOUIBONZFGZHI
    ZURUTHIZKGZLABMOAUOLZUKUQUNUSVEUJUPHAUOBFPQVEULURUMKAUOJRSTBUTLZUQVBUSVDVFU
    PVAHBUTUOFUAQVFUMVCURKBUTHRUBTUOUTAMCUCUDBOEUEUDUFUG $.
    $( [25-Oct-1999] $)

  ${
    normsub.1 $e |- A e. ~H $.
    normsub.2 $e |- B e. ~H $.
    $( Negative doesn't change the norm of a Hilbert space vector. $)
    normsubi $p |- ( normh ` ( A -h B ) ) = ( normh ` ( B -h A ) ) $=
      ( cmv co cno cfv c1 cneg cabs cmul csm hvnegdii fveq2i ax1cn negcli
      hvsubcli norm-iii.i eqtr3i absnegi cc0 cle wbr wceq 0re 1re lt01 ltleii
      absidi ax-mp eqtri opreq1i normcli recni mulid2i 3eqtri ) ABEFZGHZIJZKHZB
      AEFZGHZLFZIVCLFVCUTVBMFZGHUSVDVEURGBADCNOUTVBIPQBADCRZSTVAIVCLVAIKHZIIPUA
      UBIUCUDVGIUEUBIUFUGUHUIIUGUJUKULUMVCVCVBVFUNUOUPUQ $.
      $( [11-Aug-1999] $)

    $( Analogy to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
       [Beran] p. 98. $)
    normpythi $p |- ( ( A .ih B ) = 0 -> ( ( normh ` ( A +h B ) ) ^ 2 ) =
                   ( ( ( normh ` A ) ^ 2 ) + ( ( normh ` B ) ^ 2 ) ) ) $=
      ( csp co cc0 wceq cva caddc cno cfv c2 cexp id chil wcel wb orthcom
      mp2an biimpi opreq12d 0cn addid1i syl6eq opreq2d hicli addcli normlem8
      syl5eq hvaddcli normsqi opreq12i 3eqtr4g ) ABEFZGHZABIFZUQEFZAAEFZBBEFZJF
      ZUQKLMNFAKLMNFZBKLMNFZJFUPVAUOBAEFZJFZJFZVAURUPVFVAGJFVAUPVEGVAJUPVEGGJFG
      UPUOGVDGJUPOUPVDGHZAPQBPQUPVGRCDABSTUAUBGUCUDUEUFVAUSUTAACCUGBBDDUGUHUDUE
      ABABCDCDUIUJUQABCDUKULVBUSVCUTJACULBDULUMUN $.
      $( [17-Oct-1999] $)
  $}

  $( Swapping order of subtraction doesn't change the norm of a vector. $)
  normsub $p |- ( ( A e. ~H /\ B e. ~H ) ->
                 ( normh ` ( A -h B ) ) = ( normh ` ( B -h A ) ) ) $=
    ( chil wcel cmv co cno cfv wceq c0v cif opreq1 fveq2d opreq2 eqeq12d
    ax-hv0cl elimel normsubi dedth2h ) ACDZBCDZABEFZGHZBAEFZGHZITAJKZBEFZGHZBUF
    EFZGHZIUFUABJKZEFZGHZUKUFEFZGHZIABJJAUFIZUCUHUEUJUPUBUGGAUFBELMUPUDUIGAUFBE
    NMOBUKIZUHUMUJUOUQUGULGBUKUFENMUQUIUNGBUKUFELMOUFUKAJCPQBJCPQRS $.
    $( [14-Aug-1999] $)

  $( The norm of a vector equals the norm of its negative. $)
  normneg $p |- ( A e. ~H ->
                 ( normh ` ( -u 1 .h A ) ) = ( normh ` A ) ) $=
    ( chil wcel c0v cmv co cno cfv c1 cneg csm wceq ax-hv0cl normsub mpan
    hv2neg fveq2d hvsub0 3eqtr3d ) ABCZDAEFZGHZADEFZGHZIJAKFZGHAGHDBCTUBUDLMDAN
    OTUAUEGAPQTUCAGARQS $.
    $( [10-Jun-2005] $) $( [23-May-2005] $)

  $( Analogy to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
     [Beran] p. 98. $)
  normpyth $p |- ( ( A e. ~H /\ B e. ~H ) ->
                 ( ( A .ih B ) = 0 -> ( ( normh ` ( A +h B ) ) ^ 2 ) =
                 ( ( ( normh ` A ) ^ 2 ) + ( ( normh ` B ) ^ 2 ) ) ) ) $=
    ( chil wcel csp co cc0 wceq cva cno cfv c2 cexp caddc wi c0v cif opreq1
    eqeq1d fveq2d opreq1d fveq2 eqeq12d imbi12d opreq2 opreq2d ax-hv0cl elimel
    normpythi dedth2h ) ACDZBCDZABEFZGHZABIFZJKZLMFZAJKZLMFZBJKZLMFZNFZHZOUKAPQ
    ZBEFZGHZVDBIFZJKZLMFZVDJKZLMFZVANFZHZOVDULBPQZEFZGHZVDVNIFZJKZLMFZVKVNJKZLM
    FZNFZHZOABPPAVDHZUNVFVCVMWDUMVEGAVDBERSWDUQVIVBVLWDUPVHLMWDUOVGJAVDBIRTUAWD
    USVKVANWDURVJLMAVDJUBUAUAUCUDBVNHZVFVPVMWCWEVEVOGBVNVDEUESWEVIVSVLWBWEVHVRL
    MWEVGVQJBVNVDIUETUAWEVAWAVKNWEUTVTLMBVNJUBUAUFUCUDVDVNAPCUGUHBPCUGUHUIUJ $.
    $( [17-Oct-1999] $)

  $( Corollary to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
     [Beran] p. 98. $)
  normpyc $p |- ( ( A e. ~H /\ B e. ~H ) ->
            ( ( A .ih B ) = 0 -> ( normh ` A ) <_ ( normh ` ( A +h B ) ) ) ) $=
    ( chil wcel wa csp co cc0 wceq cno cfv c2 cexp cva cle wbr caddc cc cr
    normcl resqcl syl recnd addid1 adantr sqge0 adantl wb 0re leadd2 mp3an1
    ancoms syl2an mpbid eqbrtrrd normpyth imp breqtrrd ex le2sq normge0
    hvaddcl syl2anc sylibrd ) ACDZBCDZEZABFGHIZAJKZLMGZABNGZJKZLMGZOPZVIVLOPZVG
    VHVNVGVHEVJVJBJKZLMGZQGZVMOVGVJVROPVHVGVJHQGZVJVROVEVSVJIZVFVEVJRDVTVEVJVEV
    ISDZVJSDZATZVIUAUBZUCVJUDUBUEVGHVQOPZVSVROPZVFWEVEVFVPSDZWEBTZVPUFUBUGWBVQS
    DZWEWFUHZVEVFWIWBWJHSDWIWBWJUIHVQVJUJUKULWDVFWGWIWHVPUAUBUMUNUOUEVGVHVMVRIA
    BUPUQURUSWAHVIOPZVLSDZHVLOPZVOVNUHVGVIVLUTVEWAVFWCUEVEWKVFAVAUEVGVKCDZWLABV
    BZVKTUBVGWNWMWOVKVAUBVCVD $.
    $( [26-Oct-1999] $)

  ${
    norm3dif.1 $e |- A e. ~H $.
    norm3dif.2 $e |- B e. ~H $.
    norm3dif.3 $e |- C e. ~H $.
    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3difi $p |- ( normh ` ( A -h B ) ) <_ ( ( normh ` ( A -h C ) ) +
                   ( normh ` ( C -h B ) ) ) $=
      ( cmv co cno cfv cva caddc cle c1 cneg csm ax1cn negcli hvmulcli
      hvaddcli hvassi c0v hvcomi hvsubvali chil wcel wceq hvsubid ax-mp
      3eqtr2i opreq1i ax-hv0cl ax-hvaddid 3eqtri eqtr3i opreq2i eqtr2i
      opreq12i 3eqtr4i fveq2i hvsubcli norm-ii.i eqbrtri ) ABGHZIJACGHZCBGHZKHZ
      IJVEIJVFIJLHMVDVGIANOZBPHZKHZAVHCPHZKHZCVIKHZKHZVDVGVNAVKVMKHZKHVJAVKVMDV
      HCNQRZFSZCVIFVHBVPESZTUAVOVIAKVKCKHZVIKHZVOVIVKCVIVQFVRUAVTUBVIKHVIUBKHZV
      IVSUBVIKVSCVKKHCCGHZUBVKCVQFUCCCFFUDCUEUFWBUBUGFCUHUIUJUKUBVIULVRUCVIUEUF
      WAVIUGVRVIUMUIUNUOUPUQABDEUDVEVLVFVMKACDFUDCBFEUDURUSUTVEVFACDFVACBFEVAVB
      VC $.
      $( [16-Aug-1999] $)

    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3adifii $p |- ( abs ` ( ( normh ` ( A -h C ) ) -
         ( normh ` ( B -h C ) ) ) ) <_ ( normh ` ( A -h B ) ) $=
      ( cmv co cno cfv cmin cabs cle wbr cneg hvsubcli normcli resubcli abslei
      recni negsubdi2i caddc norm3difi normsubi opreq1i breqtri lesubaddi
      mpbir eqbrtri lenegcon1i mpbi mpbir2an ) ACGHZIJZBCGHZIJZKHZLJABGHZIJZMNU
      SOUQMNZUQUSMNZUQUSUNUPUMACDFPQZUOBCEFPQZRZURABDEPQZSUQOZUSMNUTVFUPUNKHZUS
      MUNUPUNVBTUPVCTUAVGUSMNUPUSUNUBHZMNUPBAGHIJZUNUBHVHMBCAEFDUCVIUSUNUBBAEDU
      DUEUFUPUNUSVCVBVEUGUHUIUQUSVDVEUJUKVAUNUSUPUBHMNACBDFEUCUNUPUSVBVCVEUGUHU
      L $.
      $( [30-Sep-1999] $)

    ${
      norm3lem.4 $e |- D e. RR $.
      $( Lemma involving norm of differences in Hilbert space. $)
      norm3lem $p |- ( ( ( normh ` ( A -h C ) ) < ( D / 2 ) /\
                         ( normh ` ( C -h B ) ) < ( D / 2 ) ) ->
                         ( normh ` ( A -h B ) ) < D ) $=
        ( cmv co cno cfv c2 cdiv clt wbr wa caddc hvsubcli normcli 2re 2ne0
        redivcli lt2addi cle norm3difi readdcli lelttri mpan syl cmul recni
        2timesi 2cn divcan2i eqtr3i syl6breq ) ACIJZKLZDMNJZOPCBIJZKLZUTOPQZABI
        JZKLZUTUTRJZDOVCUSVBRJZVFOPZVEVFOPZUSVBUTUTURACEGSTZVACBGFSTZDMHUAUBUCZ
        VLUDVEVGUEPVHVIABCEFGUFVEVGVFVDABEFSTUSVBVJVKUGUTUTVLVLUGUHUIUJMUTUKJVF
        DUTUTVLULUMDMDHULUNUBUOUPUQ $.
        $( [18-Aug-1999] $)
    $}
  $}

  $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
     p. 101. $)
  norm3dif $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) -> ( normh ` ( A -h B ) )
                  <_ ( ( normh ` ( A -h C ) ) + ( normh ` ( C -h B ) ) ) ) $=
    ( chil wcel cmv co cno cfv caddc cle wbr c0v cif wceq opreq1 fveq2d
    opreq1d breq12d opreq2 opreq2d opreq12d breq2d ax-hv0cl elimel norm3difi
    dedth3h ) ADEZBDEZCDEZABFGZHIZACFGZHIZCBFGZHIZJGZKLUHAMNZBFGZHIZURCFGZHIZUP
    JGZKLURUIBMNZFGZHIZVBCVDFGZHIZJGZKLVFURUJCMNZFGZHIZVJVDFGZHIZJGZKLABCMMMAUR
    OZULUTUQVCKVPUKUSHAURBFPQVPUNVBUPJVPUMVAHAURCFPQRSBVDOZUTVFVCVIKVQUSVEHBVDU
    RFTQVQUPVHVBJVQUOVGHBVDCFTQUASCVJOZVIVOVFKVRVBVLVHVNJVRVAVKHCVJURFTQVRVGVMH
    CVJVDFPQUBUCURVDVJAMDUDUEBMDUDUECMDUDUEUFUG $.
    $( [11-May-2006] $) $( [20-Apr-2006] $)

  $( Norm of differences around common element. $)
  norm3dif2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) -> ( normh ` ( A -h B )
                  ) <_ ( ( normh ` ( C -h A ) ) + ( normh ` ( C -h B ) ) ) ) $=
    ( chil wcel w3a cmv co cno cfv caddc cle norm3dif wceq normsub 3adant2
    opreq1d breqtrd ) ADEZBDEZCDEZFZABGHIJACGHIJZCBGHIJZKHCAGHIJZUDKHLABCMUBUCU
    EUDKSUAUCUENTACOPQR $.
    $( [20-Apr-2007] $) $( [18-Apr-2007] $)

  $( Lemma involving norm of differences in Hilbert space. $)
  norm3lemt $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\
                   D e. RR ) ) -> (
          ( ( normh ` ( A -h C ) ) < ( D / 2 ) /\
            ( normh ` ( C -h B ) ) < ( D / 2 ) ) ->
            ( normh ` ( A -h B ) ) < D ) ) $=
    ( chil wcel cr cmv co cno cfv c2 cdiv clt wbr wa wi c0v cif wceq opreq1
    fveq2d breq1d anbi1d imbi12d opreq2 anbi2d anbi12d imbi1d breq2d breq2
    ax-hv0cl elimel 2re norm3lem dedth4h ) AEFZBEFZCEFZDGFZACHIZJKZDLMIZNOZCBHI
    ZJKZVCNOZPZABHIZJKZDNOZQUQARSZCHIZJKZVCNOZVGPZVLBHIZJKZDNOZQVOCURBRSZHIZJKZ
    VCNOZPZVLVTHIZJKZDNOZQVLUSCRSZHIZJKZVCNOZWHVTHIZJKZVCNOZPZWGQWJUTDLSZLMIZNO
    ZWMWQNOZPZWFWPNOZQABCDRRRLAVLTZVHVPVKVSXBVDVOVGXBVBVNVCNXBVAVMJAVLCHUAUBUCU
    DXBVJVRDNXBVIVQJAVLBHUAUBUCUEBVTTZVPWDVSWGXCVGWCVOXCVFWBVCNXCVEWAJBVTCHUFUB
    UCUGXCVRWFDNXCVQWEJBVTVLHUFUBUCUECWHTZWDWOWGXDVOWKWCWNXDVNWJVCNXDVMWIJCWHVL
    HUFUBUCXDWBWMVCNXDWAWLJCWHVTHUAUBUCUHUIDWPTZWOWTWGXAXEWKWRWNWSXEVCWQWJNDWPL
    MUAZUJXEVCWQWMNXFUJUHDWPWFNUKUEVLVTWHWPAREULUMBREULUMCREULUMDLGUNUMUOUP $.
    $( [18-Aug-1999] $)

  ${
    norm3adift.1 $e |- C e. ~H $.
    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3adifi $p |- ( ( A e. ~H /\ B e. ~H ) ->
         ( abs ` ( ( normh ` ( A -h C ) ) -
         ( normh ` ( B -h C ) ) ) ) <_ ( normh ` ( A -h B ) ) ) $=
      ( chil wcel cmv co cno cfv cmin cabs cle wbr c0v cif wceq opreq1 fveq2d
      opreq1d breq12d opreq2d opreq2 ax-hv0cl elimel norm3adifii dedth2h ) AEFZ
      BEFZACGHZIJZBCGHZIJZKHZLJZABGHZIJZMNUHAOPZCGHZIJZUMKHZLJZURBGHZIJZMNUTUIB
      OPZCGHZIJZKHZLJZURVEGHZIJZMNABOOAURQZUOVBUQVDMVLUNVALVLUKUTUMKVLUJUSIAURC
      GRSTSVLUPVCIAURBGRSUABVEQZVBVIVDVKMVMVAVHLVMUMVGUTKVMULVFIBVECGRSUBSVMVCV
      JIBVEURGUCSUAURVECAOEUDUEBOEUDUEDUFUG $.
      $( [3-Oct-1999] $)
  $}

  ${
    normpar.1 $e |- A e. ~H $.
    normpar.2 $e |- B e. ~H $.
    $( Parallelogram law for norms.  Remark 3.4(B) of [Beran] p. 98. $)
    normpari $p |- ( ( ( normh ` ( A -h B ) ) ^ 2 ) +
                    ( ( normh ` ( A +h B ) ) ^ 2 ) ) =
                  ( ( 2 x. ( ( normh ` A ) ^ 2 ) ) +
                    ( 2 x. ( ( normh ` B ) ^ 2 ) ) ) $=
      ( cmv co csp cva caddc cno cfv c2 cexp cmul cneg cmin normlem9 hicli
      addcli negsubi eqtr4i normlem8 opreq12i negcli add42i cc0 negidi opreq2i
      addid1i add4i 3eqtri hvsubcli normsqi hvaddcli 2timesi eqtri 3eqtr4i ) AB
      EFZURGFZABHFZUTGFZIFZAAGFZVCIFZBBGFZVEIFZIFZURJKLMFZUTJKLMFZIFLAJKLMFZNFZ
      LBJKLMFZNFZIFVBVCVEIFZABGFZBAGFZIFZOZIFZVNVQIFZIFVNVNIFZVQVRIFZIFZVGUSVSV
      AVTIUSVNVQPFVSABABCDCDQVNVQVCVEAACCRZBBDDRZSZVOVPABCDRBADCRSZTUAABABCDCDU
      BUCVNVRVNVQWFVQWGUDWFWGUEWCWAUFIFWAVGWBUFWAIVQWGUGUHWAVNVNWFWFSUIVCVEVCVE
      WDWEWDWEUJUKUKVHUSVIVAIURABCDULUMUTABCDUNUMUCVKVDVMVFIVKLVCNFVDVJVCLNACUM
      UHVCWDUOUPVMLVENFVFVLVELNBDUMUHVEWEUOUPUCUQ $.
      $( [21-Aug-1999] $)
  $}

  $( Parallelogram law for norms.  Remark 3.4(B) of [Beran] p. 98. $)
  normpar $p |- ( ( A e. ~H /\ B e. ~H ) ->
          ( ( ( normh ` ( A -h B ) ) ^ 2 ) + ( ( normh ` ( A +h B ) ) ^ 2 ) ) =
       ( ( 2 x. ( ( normh ` A ) ^ 2 ) ) + ( 2 x. ( ( normh ` B ) ^ 2 ) ) ) ) $=
    ( chil wcel cmv co cno cfv c2 cexp cva caddc cmul wceq c0v cif opreq1
    fveq2d opreq1d opreq12d fveq2 opreq2d eqeq12d opreq2 ax-hv0cl elimel
    normpari dedth2h ) ACDZBCDZABEFZGHZIJFZABKFZGHZIJFZLFZIAGHZIJFZMFZIBGHZIJFZ
    MFZLFZNUIAOPZBEFZGHZIJFZVEBKFZGHZIJFZLFZIVEGHZIJFZMFZVCLFZNVEUJBOPZEFZGHZIJ
    FZVEVQKFZGHZIJFZLFZVOIVQGHZIJFZMFZLFZNABOOAVENZUQVLVDVPWIUMVHUPVKLWIULVGIJW
    IUKVFGAVEBEQRSWIUOVJIJWIUNVIGAVEBKQRSTWIUTVOVCLWIUSVNIMWIURVMIJAVEGUASUBSUC
    BVQNZVLWDVPWHWJVHVTVKWCLWJVGVSIJWJVFVRGBVQVEEUDRSWJVJWBIJWJVIWAGBVQVEKUDRST
    WJVCWGVOLWJVBWFIMWJVAWEIJBVQGUASUBUBUCVEVQAOCUEUFBOCUEUFUGUH $.
    $( [27-Apr-2007] $) $( [15-Apr-2007] $)

  ${
    normpar2.1 $e |- A e. ~H $.
    normpar2.2 $e |- B e. ~H $.
    normpar2.3 $e |- C e. ~H $.
    $( Corollary of parallelogram law for norms.  Part of Lemma 3.6 of [Beran]
       p. 100. $)
    normpar2i $p |- ( ( normh ` ( A -h B ) ) ^ 2 ) =
                   ( ( ( 2 x. ( ( normh ` ( A -h C ) ) ^ 2 ) ) +
                       ( 2 x. ( ( normh ` ( B -h C ) ) ^ 2 ) ) )
          - ( ( normh ` ( ( A +h B ) -h ( 2 .h C ) ) ) ^ 2 ) ) $=
      ( c2 cmv co cno cfv cexp cmul caddc cva csm cmin 2re hvsubcli normcli
      resqcli remulcli readdcli recni hvaddcli 2cn hvmulcli c4 cdiv 4re mulcli
      2ne0 divdiri addcomi cabs c1 cneg hvsubvali opreq1i hvcomi hvnegdii
      opreq12i hvsubcan2i eqtri ax1cn negcli hvadd23i 3eqtr4i 3eqtri
      hvsubdistr1i eqtr4i fveq2i norm-iii.i cc0 cle wbr wceq 0re 2pos ltleii
      absidi ax-mp sqmuli sq2 normpari 3eqtr2i divcan3i div23i 4d2e2 3eqtr3i
      subaddrii eqcomi ) GACHIZJKZGLIZMIZGBCHIZJKZGLIZMIZNIZABOIZGCPIZHIZJKZGLI
      ZQIABHIZJKZGLIZXAXFXIXAWPWTGWORWNWMACDFSZTZUAZUBGWSRWRWQBCEFSZTZUAZUBUCUD
      XFXEXDXBXCABDEUEZGCUFFUGZSZTUAUDZXIXHXGABDESZTUAUDZUHWOMIZUHWSMIZNIZGUIIZ
      YBGUIIZYCGUIIZNIXFXINIZXAYBYCGUHWOUHUJUDZWOXLUDZUKZUHWSYIWSXOUDZUKZUFULUM
      YEGXFMIZGXIMIZNIZGUIIYNGUIIZYOGUIIZNIYHYDYPGUIYDYCYBNIXDXGHIZJKZGLIZXDXGO
      IZJKZGLIZNIYPYBYCYKYMUNUUAYCUUDYBNUUAGWRMIZGLIGGLIZWSMIYCYTUUEGLYTGWQPIZJ
      KGUOKZWRMIUUEYSUUGJYSGBPIZXCHIZUUGYSXDUPUQZXGPIZOIXBUUKXCPIZOIZUULOIZUUJX
      DXGXRXTURXDUUNUULOXBXCXPXQURZUSXBUULOIZUUMOIUUIUUMOIUUOUUJUUQUUIUUMOUUQBA
      OIZBAHIZOIUUIXBUURUULUUSOABDEUTABDEVAVBBAEDVCVDUSXBUUMUULXPUUKXCUPVEVFZXQ
      UGZUUKXGUUTXTUGVGUUIXCGBUFEUGXQURVHVIGBCUFEFVJVKVLGWQUFXMVMUUHGWRMVNGVOVP
      UUHGVQVNGVRRVSVTGRWAWBZUSVIUSGWRUFWRXNUDWCUUFUHWSMWDUSVIUUDGWNMIZGLIUUFWO
      MIYBUUCUVCGLUUCGWMPIZJKUUHWNMIUVCUUBUVDJUUBGAPIZXCHIZUVDUUBUUNXGOIXBXGOIZ
      UUMOIZUVFXDUUNXGOUUPUSXBUUMXGXPUVAXTVGUVHUVEUUMOIUVFUVGUVEUUMOABDEVCUSUVE
      XCGAUFDUGXQURVKVIGACUFDFVJVKVLGWMUFXJVMUUHGWNMUVBUSVIUSGWNUFWNXKUDWCUUFUH
      WOMWDUSVIVBXDXGXRXTWEWFUSYNYOGGXFUFXSUKGXIUFYAUKUFULUMYQXFYRXINXFGXSUFULW
      GXIGYAUFULWGVBVIYFWPYGWTNYFUHGUIIZWOMIWPUHWOGYIYJUFULWHUVIGWOMWIUSVDYGUVI
      WSMIWTUHWSGYIYLUFULWHUVIGWSMWIUSVDVBWJWKWL $.
      $( [5-Oct-1999] $)
  $}

  ${
    polid2.1 $e |- A e. ~H $.
    polid2.2 $e |- B e. ~H $.
    polid2.3 $e |- C e. ~H $.
    polid2.4 $e |- D e. ~H $.
    $( Generalized polarization identity.  Generalization of Exercise 4(a) of
       [ReedSimon] p. 63. $)
    polid2i $p |- ( A .ih B ) = ( ( ( ( ( A +h C ) .ih ( D +h B ) ) -
                ( ( A -h C ) .ih ( D -h B ) ) ) +
       ( _i x. ( ( ( A +h ( _i .h C ) ) .ih ( D +h ( _i .h B ) ) ) -
               ( ( A -h ( _i .h C ) ) .ih ( D -h ( _i .h B ) ) ) ) ) ) / 4 ) $=
      ( c4 csp co cmul cdiv cva cmv cmin ci csm caddc hicli 4re recni 4pos
      gt0ne0ii divcan3i c2 2cn addcli subcli adddii cc wcel wceq ppncan mp3an
      2timesi eqtr4i opreq2i mulassi 2t2e4 opreq1i 3eqtr2ri pnncani normlem8
      normlem9 opreq12i 3eqtr4i cneg axicn hvmulcli ccj cfv chil his5 cji
      eqtri ax-his3 eqtr3i 3eqtri negcli mulcli mul12i c1 mulneg2i ixi negeqi
      ax1cn negnegi mulid2i 3eqtr3i mulm1i negsubi 3eqtr2i ) IABJKZLKZIMKWNACNK
      DBNKJKZACOKDBOKJKZPKZQAQCRKZNKDQBRKZNKJKZAWSOKDWTOKJKZPKZLKZSKZIMKWNIABEF
      TZIUAUBIUAUCUDUEWOXEIMUFWNCDJKZSKZWNXGPKZSKZLKZUFXHLKZUFXILKZSKWOXEUFXHXI
      UGWNXGXFCDGHTZUHZWNXGXFXNUIUJXKUFUFWNLKZLKUFUFLKZWNLKWOXJXPUFLXJWNWNSKZXP
      WNUKULZXGUKULXSXJXRUMXFXNXFWNXGWNUNUOWNXFUPUQURUFUFWNUGUGXFUSXQIWNLUTVAVB
      WRXLXDXMSADJKZCBJKZSKZXHSKZYBXHPKZPKXHXHSKWRXLYBXHXHXTYAADEHTZCBGFTUHXOXO
      VCWPYCWQYDPACDBEGHFVDACDBEGHFVEVFXHXOUPVGXDQUFQVHZWNLKZQXGLKZSKZLKZLKUFQY
      ILKZLKXMXCYJQLXCXTWSWTJKZSKZAWTJKZWSDJKZSKZSKZYMYPPKZPKYPYPSKZYJXAYQXBYRP
      AWSDWTEQCVIGVJZHQBVIFVJZVDAWSDWTEYTHUUAVEVFYMYPYPXTYLYEWSWTYTUUATUHYNYOAW
      TEUUATWSDYTHTUHZUUBVCUFYPLKYSYJYPUUBUPYPYIUFLYNYGYOYHSYNQVKVLZWNLKZYGQUKU
      LZAVMULBVMULYNUUDUMVIEFQABVNUOUUCYFWNLVOVAVPUUECVMULDVMULYOYHUMVIGHQCDVQU
      OVFURVRVSURUFQYIUGVIYGYHYFWNQVIVTZXFWAZQXGVIXNWAZUHWBYKXIUFLYKQYGLKZQYHLK
      ZSKWNXGVHZSKXIQYGYHVIUUGUUHUJUUIWNUUJUUKSQYFLKZWNLKWCWNLKUUIWNUULWCWNLUUL
      QQLKZVHWCVHZVHWCQQVIVIWDUUMUUNWEWFWCWGWHVSVAQYFWNVIUUFXFUSWNXFWIWJUUMXGLK
      UUNXGLKUUJUUKUUMUUNXGLWEVAQQXGVIVIXNUSXGXNWKWJVFWNXGXFXNWLVSURWMVFVGVAVR
      $.
      $( [11-Jul-2005] $) $( [30-Jun-2005] $)
  $}

  ${
    polid.1 $e |- A e. ~H $.
    polid.2 $e |- B e. ~H $.
    $( Polarization identity.  Recovers inner product from norm.  Exercise 4(a)
       of [ReedSimon] p. 63.  The outermost operation is + instead of - due to
       our mathematicians' (rather than physicists') version of axiom
       ~ ax-his3 . $)
    polidi $p |- ( A .ih B ) = ( ( ( ( ( normh ` ( A +h B ) ) ^ 2 ) -
                ( ( normh ` ( A -h B ) ) ^ 2 ) ) +
       ( _i x. ( ( ( normh ` ( A +h ( _i .h B ) ) ) ^ 2 ) -
                ( ( normh ` ( A -h ( _i .h B ) ) ) ^ 2 ) ) ) ) / 4 ) $=
      ( csp co cva cmv cmin ci csm cmul caddc c4 cdiv cno cfv c2 cexp polid2i
      hvaddcli normsqi hvsubcli opreq12i axicn hvmulcli opreq2i opreq1i eqtr4i
      ) ABEFABGFZUJEFZABHFZULEFZIFZJAJBKFZGFZUPEFZAUOHFZUREFZIFZLFZMFZNOFUJPQRS
      FZULPQRSFZIFZJUPPQRSFZURPQRSFZIFZLFZMFZNOFABBACDDCTVJVBNOVEUNVIVAMVCUKVDU
      MIUJABCDUAUBULABCDUCUBUDVHUTJLVFUQVGUSIUPAUOCJBUEDUFZUAUBURAUOCVKUCUBUDUG
      UDUHUI $.
      $( [20-Jul-2005] $) $( [30-Jun-2005] $)
  $}

  $( Polarization identity.  Recovers inner product from norm.  Exercise 4(a)
     of [ReedSimon] p. 63.  The outermost operation is + instead of - due to
     our mathematicians' (rather than physicists') version of axiom
     ~ ax-his3 . $)
  polid $p |- ( ( A e. ~H /\ B e. ~H ) ->
           ( A .ih B ) = ( ( ( ( ( normh ` ( A +h B ) ) ^ 2 ) -
              ( ( normh ` ( A -h B ) ) ^ 2 ) ) +
       ( _i x. ( ( ( normh ` ( A +h ( _i .h B ) ) ) ^ 2 ) -
                ( ( normh ` ( A -h ( _i .h B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
    ( chil wcel csp co cva cno cfv c2 cexp cmv cmin ci csm cmul caddc c4 cdiv
    wceq c0v cif opreq1 fveq2d opreq1d opreq12d opreq2d eqeq12d opreq2
    ax-hv0cl elimel polidi dedth2h ) ACDZBCDZABEFZABGFZHIZJKFZABLFZHIZJKFZMFZNA
    NBOFZGFZHIZJKFZAVDLFZHIZJKFZMFZPFZQFZRSFZTUNAUAUBZBEFZVOBGFZHIZJKFZVOBLFZHI
    ZJKFZMFZNVOVDGFZHIZJKFZVOVDLFZHIZJKFZMFZPFZQFZRSFZTVOUOBUAUBZEFZVOWNGFZHIZJ
    KFZVOWNLFZHIZJKFZMFZNVONWNOFZGFZHIZJKFZVOXCLFZHIZJKFZMFZPFZQFZRSFZTABUAUAAV
    OTZUPVPVNWMAVOBEUCXNVMWLRSXNVCWCVLWKQXNUSVSVBWBMXNURVRJKXNUQVQHAVOBGUCUDUEX
    NVAWAJKXNUTVTHAVOBLUCUDUEUFXNVKWJNPXNVGWFVJWIMXNVFWEJKXNVEWDHAVOVDGUCUDUEXN
    VIWHJKXNVHWGHAVOVDLUCUDUEUFUGUFUEUHBWNTZVPWOWMXMBWNVOEUIXOWLXLRSXOWCXBWKXKQ
    XOVSWRWBXAMXOVRWQJKXOVQWPHBWNVOGUIUDUEXOWAWTJKXOVTWSHBWNVOLUIUDUEUFXOWJXJNP
    XOWFXFWIXIMXOWEXEJKXOWDXDHXOVDXCVOGBWNNOUIZUGUDUEXOWHXHJKXOWGXGHXOVDXCVOLXP
    UGUDUEUFUGUFUEUHVOWNAUACUJUKBUACUJUKULUM $.
    $( [24-Dec-2007] $) $( [17-Nov-2007] $)

