$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Norms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Define the function for the norm of a vector of Hilbert space.  See
       ~ normvalt for its value and ~ normclt for its closure.  Theorems
       ~ norm-i , ~ norm-ii , and ~ norm-iii show it has the expected
       properties of a norm.  In the literature, the norm of ` A ` is usually
       written "|| ` A ` ||", but we use function notation to take advantage
       of our existing theorems about functions.  Definition of norm
       in [Beran] p. 96. $)
    df-hnorm $a |- normh =
                  { <. x , y >. | ( x e. H~ /\ y = ( sqr ` ( x .ih x ) ) ) } $.

    $( The norm function maps from Hilbert space to reals. $)
    normf $p |- normh : H~ --> RR $=
      ( vx vy chil cr cv csp co csqr cfv cno df-hnorm wcel cc0 cle wbr sqrclt
      hiidrclt hiidge0t sylanc fopab ) ABCDAEZUAFGZHIZJABKUBDLMUBNOUCDLUACLUBPU
      AQUARST $.
      $( [6-Sep-2007] $) $( [6-Sep-2007] $)
  $}

  ${
    $d x y A $.
    $( The value of the norm of a vector in Hilbert space.  Definition of
       norm in [Beran] p. 96.  In the literature, the norm of ` A ` is
       usually written as "|| ` A ` ||", but we use function value notation
       to take advantage of our existing theorems about functions. $)
    normvalt $p |- ( A e. H~ -> ( normh ` A ) = ( sqr ` ( A .ih A ) ) ) $=
      ( vx vy cv csp co csqr cfv chil cno wceq opreq12 anidms fveq2d df-hnorm
      fvex fvopab4 ) BCABDZREFZGHAAEFZGHIJRAKZSTGUASTKRARAELMNBCOTGPQ $.
      $( [29-May-1999] $)
  $}

  $( Real closure of the norm of a vector. $)
  normclt $p |- ( A e. H~ -> ( normh ` A ) e. RR ) $=
    ( chil cr cno normf ffvelrni ) BCADEF $.
    $( [29-May-1999] $)

  $( The norm of a vector is nonnegative. $)
  normge0t $p |- ( A e. H~ -> 0 <_ ( normh ` A ) ) $=
    ( chil wcel cc0 csp co csqr cfv cno cle cr wbr sqrge0t hiidrclt hiidge0t
    sylanc normvalt breqtrrd ) ABCZDAAEFZGHZAIHJTKCDTJLDUAJLSTMANAOPAQR $.
    $( [29-May-1999] $)

  $( The norm of non-zero vector is positive. $)
  normgt0tOLD $p |- ( A e. H~ -> ( -. A = 0h <-> 0 < ( normh ` A ) ) ) $=
    ( chil wcel c0v wceq wn cc0 csp co csqr cfv clt wbr cno cr wa sqrgt0t
    hiidrclt adantr wne ax-his4 df-ne sylan2br sylanc ex opreq1 hi01t
    sylan9eqr fveq2d sqr0 syl6eq wb cle sqrclt hiidge0t 0re jctir lttri3t syl
    pm3.27 syl6bi syld con2d impbid normvalt breq2d bitr4d ) ABCZADEZFZGAAHIZJK
    ZLMZGANKZLMVHVJVMVHVJVMVKOCZGVKLMZVMVHVJPVKQVHVOVJARZSVHADTVPVJAUAADUBUCUDU
    EVHVIVMVHVIVLGEZVMFZVHVIVRVHVIPZVLGJKGVTVKGJVIVHVKDAHIGADAHUFAUGUHUIUJUKUEV
    HVRVLGLMFZVSPZVSVHVLOCZGOCZPVRWBULVHWCWDVOGVKUMMWCVHVKUNVQAUOUDUPUQVLGURUSW
    AVSUTVAVBVCVDVHVNVLGLAVEVFVG $.
    $( [29-May-1999] $)

  $( The norm of non-zero vector is positive. $)
  normgt0t $p |- ( A e. H~ -> ( A =/= 0h <-> 0 < ( normh ` A ) ) ) $=
    ( chil wcel c0v wne cc0 csp co csqr cfv clt wbr cno cr wa sqrgt0t hiidrclt
    adantr ax-his4 sylanc ex wceq wn opreq1 hi01t sylan9eqr fveq2d sqr0 syl6eq
    wb cle sqrclt hiidge0t 0re jctir lttri3t syl pm3.27 syl6bi syld necon2ad
    impbid normvalt breq2d bitr4d ) ABCZADEZFAAGHZIJZKLZFAMJZKLVFVGVJVFVGVJVHNC
    ZFVHKLVJVFVGOVHPVFVLVGAQZRASTUAVFVJADVFADUBZVIFUBZVJUCZVFVNVOVFVNOZVIFIJFVQ
    VHFIVNVFVHDAGHFADAGUDAUEUFUGUHUIUAVFVOVIFKLUCZVPOZVPVFVINCZFNCZOVOVSUJVFVTW
    AVLFVHUKLVTVFVHULVMAUMTUNUOVIFUPUQVRVPURUSUTVAVBVFVKVIFKAVCVDVE $.
    $( [3-May-2006] $) $( [10-Apr-2006] $)

  $( The norm of a zero vector. $)
  norm0 $p |- ( normh ` 0h ) = 0 $=
    ( c0v cno cfv csp co csqr cc0 chil wcel wceq ax-hv0cl normvalt ax-mp hi01t
    fveq2d sqr0 3eqtr ) ABCZAADEZFCZGFCZGAHIZRTJKALMUBTUAJKUBSGFANOMPQ $.
    $( [30-May-1999] $)

  $( Theorem 3.3(i) of [Beran] p. 97. $)
  norm-it $p |- ( A e. H~ -> ( ( normh ` A ) = 0 <-> A = 0h ) ) $=
    ( chil wcel c0v wceq cno cfv cc0 wne clt wbr normgt0t cr cle wb 0re
    leltnet mp3an1 normclt normge0t sylanc bitrd necon4bid bicomd ) ABCZADEAFGZ
    HEUEADUFHUEADIHUFJKZUFHIZALUFMCZHUFNKZUGUHOZUEHMCUIUJUKPHUFQRASATUAUBUCUD
    $.
    $( [29-Jul-1999] $)

  $( A norm is nonzero iff its argument is a nonzero vector. $)
  normne0t $p |- ( A e. H~ -> ( ( normh ` A ) =/= 0 <-> A =/= 0h ) ) $=
    ( chil wcel cno cfv cc0 c0v norm-it necon3bid ) ABCADEFAGAHI $.
    $( [12-Mar-2006] $) $( [11-Mar-2006] $)

  ${
    normcl.1 $e |- A e. H~ $.
    $( Real closure of the norm of a vector. $)
    normcl $p |- ( normh ` A ) e. RR $=
      ( chil wcel cno cfv cr normclt ax-mp ) ACDAEFGDBAHI $.
      $( [30-Sep-1999] $)

    $( The square of a norm. $)
    normsq $p |- ( ( normh ` A ) ^ 2 ) = ( A .ih A ) $=
      ( cno cfv c2 cexp co csp csqr chil wcel wceq normvalt ax-mp opreq1i cc0
      cle wbr hiidge0t cr hiidrclt sqsqr eqtr ) ACDZEFGAAHGZIDZEFGZUEUDUFEFAJKZ
      UDUFLBAMNOPUEQRZUGUELUHUIBASNUEUHUETKBAUANUBNUC $.
      $( [21-Aug-1999] $)

    $( Theorem 3.3(i) of [Beran] p. 97. $)
    norm-i $p |- ( ( normh ` A ) = 0 <-> A = 0h ) $=
      ( chil wcel cno cfv cc0 wceq c0v wb norm-it ax-mp ) ACDAEFGHAIHJBAKL $.
      $( [5-Sep-1999] $)
  $}

  $( The square of a norm. $)
  normsqt $p |- ( A e. H~ -> ( ( normh ` A ) ^ 2 ) = ( A .ih A ) ) $=
    ( chil wcel cno cfv c2 cexp co csp wceq c0v cif fveq2 opreq1d id opreq12d
    eqeq12d ax-hv0cl elimel normsq dedth ) ABCZADEZFGHZAAIHZJUBAKLZDEZFGHZUFUFI
    HZJAKAUFJZUDUHUEUIUJUCUGFGAUFDMNUJAUFAUFIUJOZUKPQUFAKBRSTUA $.
    $( [3-Jul-2005] $) $( [12-May-2005] $)

  ${
    normsub0.1 $e |- A e. H~ $.
    normsub0.2 $e |- B e. H~ $.
    $( Two vectors are equal iff the norm of their difference is zero. $)
    normsub0 $p |- ( ( normh ` ( A -h B ) ) = 0 <-> A = B ) $=
      ( cmv co cno cfv cc0 wceq c0v hvsubcl norm-i hvsubeq0 bitr ) ABEFZGHIJPKJ
      ABJPABCDLMABCDNO $.
      $( [18-Aug-1999] $)
  $}

  $( Two vectors are equal iff the norm of their difference is zero. $)
  normsub0t $p |- ( ( A e. H~ /\ B e. H~ ) ->
                  ( ( normh ` ( A -h B ) ) = 0 <-> A = B ) ) $=
    ( chil wcel cmv co cno cfv cc0 wceq wb c0v cif opreq1 fveq2d eqeq1d eqeq1
    bibi12d opreq2 eqeq2 ax-hv0cl elimel normsub0 dedth2h ) ACDZBCDZABEFZGHZIJZ
    ABJZKUEALMZBEFZGHZIJZUKBJZKUKUFBLMZEFZGHZIJZUKUPJZKABLLAUKJZUIUNUJUOVAUHUMI
    VAUGULGAUKBENOPAUKBQRBUPJZUNUSUOUTVBUMURIVBULUQGBUPUKESOPBUPUKTRUKUPALCUAUB
    BLCUAUBUCUD $.
    $( [18-Aug-1999] $)

  ${
    norm-ii.1 $e |- A e. H~ $.
    norm-ii.2 $e |- B e. H~ $.
    $( Triangle inequality for norms.  Theorem 3.3(ii) of [Beran] p. 97. $)
    norm-ii $p |- ( normh ` ( A +h B ) ) <_
                  ( ( normh ` A ) + ( normh ` B ) ) $=
      ( cva co csp csqr cfv caddc cno cle c2 cexp wbr cmul c1 ccj cr wcel wceq
      1re ax1cn cjreb mpbi opreq1i hicl mulid2 eqtr opreq12i cc0 cabs 0re lt01
      ltlei absid ax-mp normlem7 eqbrtrr cneg eqid normlem2 cjcl mulcl addcl
      negreb eqeltrr 2re chil hiidge0t hiidrclt sqrcl remulcl readdcl leadd2
      normlem8 addcom opreq2i recn binom2 sqcl 2cn add23 sqsqr 3eqtr 3brtr4 wb
      hvaddcl sqge0 resqcl sqrle mp2an sqrge0 addge0 sqrsq breqtr normvalt ) AB
      EFZWRGFZHIZAAGFZHIZBBGFZHIZJFZWRKIZAKIZBKIZJFLWTXEMNFZHIZXELWSXILOZWTXJLO
      ZXAXCJFZBAGFZABGFZJFZJFZXMMXBXDPFZPFZJFZWSXILXPXSLOXQXTLOQRIZXNPFZQXOPFZJ
      FZXPXSLYBXNYCXOJYBQXNPFXNYAQXNPQSTYAQUAUBQUCUDUEUFXNBADCUGZUHUIXOABCDUGZU
      HUJZQBAUCDCUKQLOQULIQUAUKQUMUBUNUOQUBUPUQURUSXPXSXMYDXPSYGYDUTZSTYDSTYHQB
      AUCDCYHVAVBYDYBYCYAXNQUCVCYEVDQXOUCYFVDVEVFUEVGMXRVHXBXDUKXALOZXBSTAVITZY
      ICAVJUQZXAYJXASTCAVKUQZVLUQZUKXCLOZXDSTBVITZYNDBVJUQZXCYOXCSTDBVKUQZVLUQZ
      VMVMXAXCYLYQVNVOUEWSXMXOXNJFZJFXQABABCDCDVPYSXPXMJXOXNYFYEVQVRUIXIXBMNFZX
      SJFXDMNFZJFYTUUAJFZXSJFXTXBXDXBYMVSZXDYRVSZVTYTXSUUAXBUUCWAMXRWBXBXDUUCUU
      DVDVDXDUUDWAWCUUBXMXSJYTXAUUAXCJYIYTXAUAYKXAYLWDUQYNUUAXCUAYPXCYQWDUQUJUF
      WEWFUKWSLOZUKXILOXKXLWGWRVITZUUEABCDWHZWRVJUQXEXBXDYMYRVNZWIWSXIUUFWSSTUU
      GWRVKUQXEUUHWJWKWLUEUKXELOZXJXEUAUKXBLOZUKXDLOZUUIYIUUJYKXAYLWMUQYNUUKYPX
      CYQWMUQXBXDYMYRWNWLXEUUHWOUQWPUUFXFWTUAUUGWRWQUQXGXBXHXDJYJXGXBUACAWQUQYO
      XHXDUADBWQUQUJWF $.
      $( [11-Aug-1999] $)
  $}

  $( Triangle inequality for norms.  Theorem 3.3(ii) of [Beran] p. 97. $)
  norm-iit $p |- ( ( A e. H~ /\ B e. H~ ) ->
               ( normh ` ( A +h B ) ) <_ ( ( normh ` A ) + ( normh ` B ) ) ) $=
    ( chil wcel cva co cno cfv caddc cle wbr c0v cif wceq opreq1 fveq2d fveq2
    opreq1d breq12d opreq2 opreq2d ax-hv0cl elimel norm-ii dedth2h ) ACDZBCDZAB
    EFZGHZAGHZBGHZIFZJKUFALMZBEFZGHZUMGHZUKIFZJKUMUGBLMZEFZGHZUPURGHZIFZJKABLLA
    UMNZUIUOULUQJVCUHUNGAUMBEOPVCUJUPUKIAUMGQRSBURNZUOUTUQVBJVDUNUSGBURUMETPVDU
    KVAUPIBURGQUASUMURALCUBUCBLCUBUCUDUE $.
    $( [11-Mar-2006] $) $( [10-Mar-2006] $)

  ${
    norm-iii.1 $e |- A e. CC $.
    norm-iii.2 $e |- B e. H~ $.
    $( Theorem 3.3(iii) of [Beran] p. 97. $)
    norm-iii $p |- ( normh ` ( A .h B ) ) = ( ( abs ` A ) x. ( normh ` B ) ) $=
      ( csm co csp csqr cfv ccj cmul cno cabs his35 fveq2i cjmulrcl chil wcel
      cr hiidrclt ax-mp cjmulge0 cc0 cle wbr hiidge0t sqrmuli eqtr wceq
      hvmulcl normvalt cc absvalt opreq12i 3eqtr4 ) ABEFZUPGFZHIZAAJIKFZHIZBBGF
      ZHIZKFZUPLIZAMIZBLIZKFURUSVAKFZHIVCUQVGHAABBCCDDNOUSVAACPBQRZVASRDBTUAACU
      BVHUCVAUDUEDBUFUAUGUHUPQRVDURUIABCDUJUPUKUAVEUTVFVBKAULRVEUTUICAUMUAVHVFV
      BUIDBUKUAUNUO $.
      $( [29-Jul-1999] $)
  $}

  $( Theorem 3.3(iii) of [Beran] p. 97. $)
  norm-iiit $p |- ( ( A e. CC /\ B e. H~ ) ->
                ( normh ` ( A .h B ) ) = ( ( abs ` A ) x. ( normh ` B ) ) ) $=
    ( cc wcel chil csm co cno cfv cabs cmul wceq cc0 cif c0v opreq1 fveq2d
    fveq2 opreq1d eqeq12d opreq2 opreq2d 0cn elimel ax-hv0cl norm-iii dedth2h
    ) ACDZBEDZABFGZHIZAJIZBHIZKGZLUHAMNZBFGZHIZUOJIZUMKGZLUOUIBONZFGZHIZURUTHIZ
    KGZLABMOAUOLZUKUQUNUSVEUJUPHAUOBFPQVEULURUMKAUOJRSTBUTLZUQVBUSVDVFUPVAHBUTU
    OFUAQVFUMVCURKBUTHRUBTUOUTAMCUCUDBOEUEUDUFUG $.
    $( [25-Oct-1999] $)

  ${
    normsub.1 $e |- A e. H~ $.
    normsub.2 $e |- B e. H~ $.
    $( Negative doesn't change the norm of a Hilbert space vector. $)
    normsub $p |- ( normh ` ( A -h B ) ) = ( normh ` ( B -h A ) ) $=
      ( cmv co cno cfv c1 cneg cabs cmul csm hvnegdi fveq2i ax1cn negcl
      hvsubcl norm-iii eqtr3 absneg cc0 cle wbr wceq 0re 1re lt01 ltlei absid
      ax-mp eqtr opreq1i normcl recn mulid2 3eqtr ) ABEFZGHZIJZKHZBAEFZGHZLFZIV
      CLFVCUTVBMFZGHUSVDVEURGBADCNOUTVBIPQBADCRZSTVAIVCLVAIKHZIIPUAUBIUCUDVGIUE
      UBIUFUGUHUIIUGUJUKULUMVCVCVBVFUNUOUPUQ $.
      $( [11-Aug-1999] $)

    $( Analogy to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
       [Beran] p. 98. $)
    normpyth $p |- ( ( A .ih B ) = 0 -> ( ( normh ` ( A +h B ) ) ^ 2 ) =
                   ( ( ( normh ` A ) ^ 2 ) + ( ( normh ` B ) ^ 2 ) ) ) $=
      ( csp co cc0 wceq cva caddc cno cfv c2 cexp id chil wcel wb orthcom
      mp2an biimp opreq12d 0cn addid1 syl6eq opreq2d hicl addcl normlem8
      syl5eq hvaddcl normsq opreq12i 3eqtr4g ) ABEFZGHZABIFZUQEFZAAEFZBBEFZJFZU
      QKLMNFAKLMNFZBKLMNFZJFUPVAUOBAEFZJFZJFZVAURUPVFVAGJFVAUPVEGVAJUPVEGGJFGUP
      UOGVDGJUPOUPVDGHZAPQBPQUPVGRCDABSTUAUBGUCUDUEUFVAUSUTAACCUGBBDDUGUHUDUEAB
      ABCDCDUIUJUQABCDUKULVBUSVCUTJACULBDULUMUN $.
      $( [17-Oct-1999] $)
  $}

  $( Swapping order of subtraction doesn't change the norm of a vector. $)
  normsubt $p |- ( ( A e. H~ /\ B e. H~ ) ->
                 ( normh ` ( A -h B ) ) = ( normh ` ( B -h A ) ) ) $=
    ( chil wcel cmv co cno cfv wceq c0v cif opreq1 fveq2d opreq2 eqeq12d
    ax-hv0cl elimel normsub dedth2h ) ACDZBCDZABEFZGHZBAEFZGHZITAJKZBEFZGHZBUFE
    FZGHZIUFUABJKZEFZGHZUKUFEFZGHZIABJJAUFIZUCUHUEUJUPUBUGGAUFBELMUPUDUIGAUFBEN
    MOBUKIZUHUMUJUOUQUGULGBUKUFENMUQUIUNGBUKUFELMOUFUKAJCPQBJCPQRS $.
    $( [14-Aug-1999] $)

  $( The norm of a vector equals the norm of its negative. $)
  normnegt $p |- ( A e. H~ ->
                 ( normh ` ( -u 1 .h A ) ) = ( normh ` A ) ) $=
    ( chil wcel c0v cmv co cno cfv c1 cneg csm wceq ax-hv0cl normsubt mpan
    hv2negt fveq2d hvsub0t 3eqtr3d ) ABCZDAEFZGHZADEFZGHZIJAKFZGHAGHDBCTUBUDLMD
    ANOTUAUEGAPQTUCAGARQS $.
    $( [10-Jun-2005] $) $( [23-May-2005] $)

  $( Analogy to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
     [Beran] p. 98. $)
  normpytht $p |- ( ( A e. H~ /\ B e. H~ ) ->
                 ( ( A .ih B ) = 0 -> ( ( normh ` ( A +h B ) ) ^ 2 ) =
                 ( ( ( normh ` A ) ^ 2 ) + ( ( normh ` B ) ^ 2 ) ) ) ) $=
    ( chil wcel csp co cc0 wceq cva cno cfv c2 cexp caddc wi c0v cif opreq1
    eqeq1d fveq2d opreq1d fveq2 eqeq12d imbi12d opreq2 opreq2d ax-hv0cl elimel
    normpyth dedth2h ) ACDZBCDZABEFZGHZABIFZJKZLMFZAJKZLMFZBJKZLMFZNFZHZOUKAPQZ
    BEFZGHZVDBIFZJKZLMFZVDJKZLMFZVANFZHZOVDULBPQZEFZGHZVDVNIFZJKZLMFZVKVNJKZLMF
    ZNFZHZOABPPAVDHZUNVFVCVMWDUMVEGAVDBERSWDUQVIVBVLWDUPVHLMWDUOVGJAVDBIRTUAWDU
    SVKVANWDURVJLMAVDJUBUAUAUCUDBVNHZVFVPVMWCWEVEVOGBVNVDEUESWEVIVSVLWBWEVHVRLM
    WEVGVQJBVNVDIUETUAWEVAWAVKNWEUTVTLMBVNJUBUAUFUCUDVDVNAPCUGUHBPCUGUHUIUJ $.
    $( [17-Oct-1999] $)

  $( Corollary to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
     [Beran] p. 98. $)
  normpyct $p |- ( ( A e. H~ /\ B e. H~ ) ->
            ( ( A .ih B ) = 0 -> ( normh ` A ) <_ ( normh ` ( A +h B ) ) ) ) $=
    ( chil wcel wa csp co cc0 wceq cno cfv c2 cexp cva cle wbr caddc cc cr
    normclt resqclt syl recnd ax0id adantr sqge0t adantl wb 0re leadd2t mp3an1
    ancoms syl2an mpbid eqbrtrrd normpytht imp breqtrrd ex le2sqt normge0t
    hvaddclt syl2anc sylibrd ) ACDZBCDZEZABFGHIZAJKZLMGZABNGZJKZLMGZOPZVIVLOPZV
    GVHVNVGVHEVJVJBJKZLMGZQGZVMOVGVJVROPVHVGVJHQGZVJVROVEVSVJIZVFVEVJRDVTVEVJVE
    VISDZVJSDZATZVIUAUBZUCVJUDUBUEVGHVQOPZVSVROPZVFWEVEVFVPSDZWEBTZVPUFUBUGWBVQ
    SDZWEWFUHZVEVFWIWBWJHSDWIWBWJUIHVQVJUJUKULWDVFWGWIWHVPUAUBUMUNUOUEVGVHVMVRI
    ABUPUQURUSWAHVIOPZVLSDZHVLOPZVOVNUHVGVIVLUTVEWAVFWCUEVEWKVFAVAUEVGVKCDZWLAB
    VBZVKTUBVGWNWMWOVKVAUBVCVD $.
    $( [26-Oct-1999] $)

  ${
    norm3dif.1 $e |- A e. H~ $.
    norm3dif.2 $e |- B e. H~ $.
    norm3dif.3 $e |- C e. H~ $.
    $( Norm of differences around common element.  Part of Lemma 3.6 of
       [Beran] p. 101. $)
    norm3dif $p |- ( normh ` ( A -h B ) ) <_ ( ( normh ` ( A -h C ) ) +
                   ( normh ` ( C -h B ) ) ) $=
      ( cmv co cno cfv cva caddc cle c1 cneg csm ax1cn negcl hvmulcl hvaddcl
      hvass c0v hvcom hvsubval chil wcel wceq hvsubidt ax-mp 3eqtr2 opreq1i
      ax-hv0cl ax-hvaddid 3eqtr eqtr3 opreq2i eqtr2 opreq12i 3eqtr4 fveq2i
      hvsubcl norm-ii eqbrtr ) ABGHZIJACGHZCBGHZKHZIJVEIJVFIJLHMVDVGIANOZBPHZKH
      ZAVHCPHZKHZCVIKHZKHZVDVGVNAVKVMKHZKHVJAVKVMDVHCNQRZFSZCVIFVHBVPESZTUAVOVI
      AKVKCKHZVIKHZVOVIVKCVIVQFVRUAVTUBVIKHVIUBKHZVIVSUBVIKVSCVKKHCCGHZUBVKCVQF
      UCCCFFUDCUEUFWBUBUGFCUHUIUJUKUBVIULVRUCVIUEUFWAVIUGVRVIUMUIUNUOUPUQABDEUD
      VEVLVFVMKACDFUDCBFEUDURUSUTVEVFACDFVACBFEVAVBVC $.
      $( [16-Aug-1999] $)

    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3adif $p |- ( abs ` ( ( normh ` ( A -h C ) ) -
         ( normh ` ( B -h C ) ) ) ) <_ ( normh ` ( A -h B ) ) $=
      ( cmv co cno cfv cmin cabs cle wbr cneg hvsubcl normcl resubcl absleOLD
      caddc norm3dif lesubadd mpbir recn negsubdi2 normsub opreq1i breqtr
      eqbrtr mpbir2an ) ACGHZIJZBCGHZIJZKHZLJABGHZIJZMNUOUQMNZUOOZUQMNUOUQULUNU
      KACDFPQZUMBCEFPQZRUPABDEPQZSURULUQUNTHMNACBDFEUAULUNUQUTVAVBUBUCUSUNULKHZ
      UQMULUNULUTUDUNVAUDUEVCUQMNUNUQULTHZMNUNBAGHIJZULTHVDMBCAEFDUAVEUQULTBAED
      UFUGUHUNULUQVAUTVBUBUCUIUJ $.
      $( [30-Sep-1999] $)

    ${
      norm3lem.4 $e |- D e. RR $.
      $( Lemma involving norm of differences in Hilbert space. $)
      norm3lem $p |- ( ( ( normh ` ( A -h C ) ) < ( D / 2 ) /\
                         ( normh ` ( C -h B ) ) < ( D / 2 ) ) ->
                         ( normh ` ( A -h B ) ) < D ) $=
        ( cmv co cno cfv c2 cdiv clt wbr wa caddc hvsubcl normcl 2re 2ne0
        redivcl lt2add cle norm3dif readdcl lelttr mpan syl cmul recn 2times
        2cn divcan2 eqtr3 syl6breq ) ACIJZKLZDMNJZOPCBIJZKLZUTOPQZABIJZKLZUTUTR
        JZDOVCUSVBRJZVFOPZVEVFOPZUSVBUTUTURACEGSTZVACBGFSTZDMHUAUBUCZVLUDVEVGUE
        PVHVIABCEFGUFVEVGVFVDABEFSTUSVBVJVKUGUTUTVLVLUGUHUIUJMUTUKJVFDUTUTVLULU
        MMDUNDHULUBUOUPUQ $.
        $( [18-Aug-1999] $)
    $}
  $}

  $( Norm of differences around common element.  Part of Lemma 3.6 of
     [Beran] p. 101. $)
  norm3dift $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) -> ( normh ` ( A -h B ) )
                  <_ ( ( normh ` ( A -h C ) ) + ( normh ` ( C -h B ) ) ) ) $=
    ( chil wcel cmv co cno cfv caddc cle wbr c0v cif wceq opreq1 fveq2d
    opreq1d breq12d opreq2 opreq2d opreq12d breq2d ax-hv0cl elimel norm3dif
    dedth3h ) ADEZBDEZCDEZABFGZHIZACFGZHIZCBFGZHIZJGZKLUHAMNZBFGZHIZURCFGZHIZUP
    JGZKLABCMMMURUIBMNZFGZHIZVBCVDFGZHIZJGZKLVFURUJCMNZFGZHIZVJVDFGZHIZJGZKLAUR
    OZULUTUQVCKVPUKUSHAURBFPQVPUNVBUPJVPUMVAHAURCFPQRSBVDOZUTVFVCVIKVQUSVEHBVDU
    RFTQVQUPVHVBJVQUOVGHBVDCFTQUASCVJOZVIVOVFKVRVBVLVHVNJVRVAVKHCVJURFTQVRVGVMH
    CVJVDFPQUBUCURVDVJAMDUDUEBMDUDUECMDUDUEUFUG $.
    $( [11-May-2006] $) $( [20-Apr-2006] $)

  $( Norm of differences around common element. $)
  norm3dif2t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) -> ( normh ` ( A -h B )
                  ) <_ ( ( normh ` ( C -h A ) ) + ( normh ` ( C -h B ) ) ) ) $=
    ( chil wcel w3a cmv co cno cfv caddc cle norm3dift wceq normsubt 3adant2
    opreq1d breqtrd ) ADEZBDEZCDEZFZABGHIJACGHIJZCBGHIJZKHCAGHIJZUDKHLABCMUBUCU
    EUDKSUAUCUENTACOPQR $.
    $( [20-Apr-2007] $) $( [18-Apr-2007] $)

  $( Lemma involving norm of differences in Hilbert space. $)
  norm3lemt $p |- ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. H~ /\
                   D e. RR ) ) -> (
          ( ( normh ` ( A -h C ) ) < ( D / 2 ) /\
            ( normh ` ( C -h B ) ) < ( D / 2 ) ) ->
            ( normh ` ( A -h B ) ) < D ) ) $=
    ( chil wcel cr cmv co cno cfv c2 cdiv clt wbr wa wi c0v cif wceq opreq1
    fveq2d breq1d anbi1d imbi12d opreq2 anbi2d anbi12d imbi1d breq2d breq2
    ax-hv0cl elimel 2re norm3lem dedth4h ) AEFZBEFZCEFZDGFZACHIZJKZDLMIZNOZCBHI
    ZJKZVCNOZPZABHIZJKZDNOZQABCDRRUQARSZCHIZJKZVCNOZVGPZVLBHIZJKZDNOZQVOCURBRSZ
    HIZJKZVCNOZPZVLVTHIZJKZDNOZQVLUSCRSZHIZJKZVCNOZWHVTHIZJKZVCNOZPZWGQWJUTDLSZ
    LMIZNOZWMWQNOZPZWFWPNOZQRLAVLTZVHVPVKVSXBVDVOVGXBVBVNVCNXBVAVMJAVLCHUAUBUCU
    DXBVJVRDNXBVIVQJAVLBHUAUBUCUEBVTTZVPWDVSWGXCVGWCVOXCVFWBVCNXCVEWAJBVTCHUFUB
    UCUGXCVRWFDNXCVQWEJBVTVLHUFUBUCUECWHTZWDWOWGXDVOWKWCWNXDVNWJVCNXDVMWIJCWHVL
    HUFUBUCXDWBWMVCNXDWAWLJCWHVTHUAUBUCUHUIDWPTZWOWTWGXAXEWKWRWNWSXEVCWQWJNDWPL
    MUAZUJXEVCWQWMNXFUJUHDWPWFNUKUEVLVTWHWPAREULUMBREULUMCREULUMDLGUNUMUOUP $.
    $( [18-Aug-1999] $)

  ${
    norm3adift.1 $e |- C e. H~ $.
    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3adift $p |- ( ( A e. H~ /\ B e. H~ ) ->
         ( abs ` ( ( normh ` ( A -h C ) ) -
         ( normh ` ( B -h C ) ) ) ) <_ ( normh ` ( A -h B ) ) ) $=
      ( chil wcel cmv co cno cfv cmin cabs cle wbr c0v cif wceq opreq1 fveq2d
      opreq1d breq12d opreq2d opreq2 ax-hv0cl elimel norm3adif dedth2h ) AEFZBE
      FZACGHZIJZBCGHZIJZKHZLJZABGHZIJZMNUHAOPZCGHZIJZUMKHZLJZURBGHZIJZMNUTUIBOP
      ZCGHZIJZKHZLJZURVEGHZIJZMNABOOAURQZUOVBUQVDMVLUNVALVLUKUTUMKVLUJUSIAURCGR
      STSVLUPVCIAURBGRSUABVEQZVBVIVDVKMVMVAVHLVMUMVGUTKVMULVFIBVECGRSUBSVMVCVJI
      BVEURGUCSUAURVECAOEUDUEBOEUDUEDUFUG $.
      $( [3-Oct-1999] $)
  $}

  ${
    normpar.1 $e |- A e. H~ $.
    normpar.2 $e |- B e. H~ $.
    $( Parallelogram law for norms.  Remark 3.4(B) of [Beran] p. 98. $)
    normpar $p |- ( ( ( normh ` ( A -h B ) ) ^ 2 ) +
                    ( ( normh ` ( A +h B ) ) ^ 2 ) ) =
                  ( ( 2 x. ( ( normh ` A ) ^ 2 ) ) +
                    ( 2 x. ( ( normh ` B ) ^ 2 ) ) ) $=
      ( cmv co csp cva caddc cno cfv c2 cexp cmul cneg cmin normlem9 hicl
      addcl negsub eqtr4 normlem8 opreq12i negcl add42 cc0 negid opreq2i
      addid1 add4 3eqtr hvsubcl normsq hvaddcl 2times eqtr 3eqtr4 ) ABEFZURGFZA
      BHFZUTGFZIFZAAGFZVCIFZBBGFZVEIFZIFZURJKLMFZUTJKLMFZIFLAJKLMFZNFZLBJKLMFZN
      FZIFVBVCVEIFZABGFZBAGFZIFZOZIFZVNVQIFZIFVNVNIFZVQVRIFZIFZVGUSVSVAVTIUSVNV
      QPFVSABABCDCDQVNVQVCVEAACCRZBBDDRZSZVOVPABCDRBADCRSZTUAABABCDCDUBUCVNVRVN
      VQWFVQWGUDWFWGUEWCWAUFIFWAVGWBUFWAIVQWGUGUHWAVNVNWFWFSUIVCVEVCVEWDWEWDWEU
      JUKUKVHUSVIVAIURABCDULUMUTABCDUNUMUCVKVDVMVFIVKLVCNFVDVJVCLNACUMUHVCWDUOU
      PVMLVENFVFVLVELNBDUMUHVEWEUOUPUCUQ $.
      $( [21-Aug-1999] $)
  $}

  $( Parallelogram law for norms.  Remark 3.4(B) of [Beran] p. 98. $)
  normpart $p |- ( ( A e. H~ /\ B e. H~ ) ->
          ( ( ( normh ` ( A -h B ) ) ^ 2 ) + ( ( normh ` ( A +h B ) ) ^ 2 ) ) =
       ( ( 2 x. ( ( normh ` A ) ^ 2 ) ) + ( 2 x. ( ( normh ` B ) ^ 2 ) ) ) ) $=
    ( chil wcel cmv co cno cfv c2 cexp cva caddc cmul wceq c0v cif opreq1
    fveq2d opreq1d opreq12d fveq2 opreq2d eqeq12d opreq2 ax-hv0cl elimel
    normpar dedth2h ) ACDZBCDZABEFZGHZIJFZABKFZGHZIJFZLFZIAGHZIJFZMFZIBGHZIJFZM
    FZLFZNUIAOPZBEFZGHZIJFZVEBKFZGHZIJFZLFZIVEGHZIJFZMFZVCLFZNVEUJBOPZEFZGHZIJF
    ZVEVQKFZGHZIJFZLFZVOIVQGHZIJFZMFZLFZNABOOAVENZUQVLVDVPWIUMVHUPVKLWIULVGIJWI
    UKVFGAVEBEQRSWIUOVJIJWIUNVIGAVEBKQRSTWIUTVOVCLWIUSVNIMWIURVMIJAVEGUASUBSUCB
    VQNZVLWDVPWHWJVHVTVKWCLWJVGVSIJWJVFVRGBVQVEEUDRSWJVJWBIJWJVIWAGBVQVEKUDRSTW
    JVCWGVOLWJVBWFIMWJVAWEIJBVQGUASUBUBUCVEVQAOCUEUFBOCUEUFUGUH $.
    $( [27-Apr-2007] $) $( [15-Apr-2007] $)

  ${
    normpar2.1 $e |- A e. H~ $.
    normpar2.2 $e |- B e. H~ $.
    normpar2.3 $e |- C e. H~ $.
    $( Corollary of parallelogram law for norms.  Part of Lemma 3.6 of [Beran]
       p. 100. $)
    normpar2 $p |- ( ( normh ` ( A -h B ) ) ^ 2 ) =
                   ( ( ( 2 x. ( ( normh ` ( A -h C ) ) ^ 2 ) ) +
                       ( 2 x. ( ( normh ` ( B -h C ) ) ^ 2 ) ) )
          - ( ( normh ` ( ( A +h B ) -h ( 2 .h C ) ) ) ^ 2 ) ) $=
      ( c2 cmv co cno cfv cexp cmul caddc cva csm cmin 2re hvsubcl normcl
      resqcl remulcl readdcl recn hvaddcl 2cn hvmulcl c4 cdiv 4re mulcl 2ne0
      divdir addcom cabs c1 cneg hvsubval opreq1i hvcom hvnegdi opreq12i
      hvsubcan2 eqtr ax1cn negcl hvadd23 3eqtr4 3eqtr hvsubdistr1 eqtr4 fveq2i
      norm-iii cc0 cle wbr wceq 0re 2pos ltlei absid ax-mp sqmul sq2 normpar
      3eqtr2 divcan3 div23 4d2e2 3eqtr3 subaddri eqcomi ) GACHIZJKZGLIZMIZGBCHI
      ZJKZGLIZMIZNIZABOIZGCPIZHIZJKZGLIZQIABHIZJKZGLIZXAXFXIXAWPWTGWORWNWMACDFS
      ZTZUAZUBGWSRWRWQBCEFSZTZUAZUBUCUDXFXEXDXBXCABDEUEZGCUFFUGZSZTUAUDZXIXHXGA
      BDESZTUAUDZUHWOMIZUHWSMIZNIZGUIIZYBGUIIZYCGUIIZNIXFXINIZXAYBYCGUHWOUHUJUD
      ZWOXLUDZUKZUHWSYIWSXOUDZUKZUFULUMYEGXFMIZGXIMIZNIZGUIIYNGUIIZYOGUIIZNIYHY
      DYPGUIYDYCYBNIXDXGHIZJKZGLIZXDXGOIZJKZGLIZNIYPYBYCYKYMUNUUAYCUUDYBNUUAGWR
      MIZGLIGGLIZWSMIYCYTUUEGLYTGWQPIZJKGUOKZWRMIUUEYSUUGJYSGBPIZXCHIZUUGYSXDUP
      UQZXGPIZOIXBUUKXCPIZOIZUULOIZUUJXDXGXRXTURXDUUNUULOXBXCXPXQURZUSXBUULOIZU
      UMOIUUIUUMOIUUOUUJUUQUUIUUMOUUQBAOIZBAHIZOIUUIXBUURUULUUSOABDEUTABDEVAVBB
      AEDVCVDUSXBUUMUULXPUUKXCUPVEVFZXQUGZUUKXGUUTXTUGVGUUIXCGBUFEUGXQURVHVIGBC
      UFEFVJVKVLGWQUFXMVMUUHGWRMVNGVOVPUUHGVQVNGVRRVSVTGRWAWBZUSVIUSGWRUFWRXNUD
      WCUUFUHWSMWDUSVIUUDGWNMIZGLIUUFWOMIYBUUCUVCGLUUCGWMPIZJKUUHWNMIUVCUUBUVDJ
      UUBGAPIZXCHIZUVDUUBUUNXGOIXBXGOIZUUMOIZUVFXDUUNXGOUUPUSXBUUMXGXPUVAXTVGUV
      HUVEUUMOIUVFUVGUVEUUMOABDEVCUSUVEXCGAUFDUGXQURVKVIGACUFDFVJVKVLGWMUFXJVMU
      UHGWNMUVBUSVIUSGWNUFWNXKUDWCUUFUHWOMWDUSVIVBXDXGXRXTWEWFUSYNYOGGXFUFXSUKG
      XIUFYAUKUFULUMYQXFYRXINGXFUFXSULWGGXIUFYAULWGVBVIYFWPYGWTNYFUHGUIIZWOMIWP
      UHWOGYIYJUFULWHUVIGWOMWIUSVDYGUVIWSMIWTUHWSGYIYLUFULWHUVIGWSMWIUSVDVBWJWK
      WL $.
      $( [5-Oct-1999] $)
  $}

  ${
    polid2.1 $e |- A e. H~ $.
    polid2.2 $e |- B e. H~ $.
    polid2.3 $e |- C e. H~ $.
    polid2.4 $e |- D e. H~ $.
    $( Generalized polarization identity.  Generalization of Exercise 4(a) of
       [ReedSimon] p. 63. $)
    polid2 $p |- ( A .ih B ) = ( ( ( ( ( A +h C ) .ih ( D +h B ) )  -
                ( ( A -h C ) .ih ( D -h B ) )  ) +
       ( i x. ( ( ( A +h ( i .h C ) ) .ih ( D +h ( i .h B ) ) ) -
                ( ( A -h ( i .h C ) ) .ih ( D -h ( i .h B ) ) ) ) ) ) / 4 ) $=
      ( c4 csp co cmul cdiv cva cmv cmin ci csm caddc 4re recn hicl 4pos
      gt0ne0i divcan3 c2 2cn addcl subcl adddi cc wcel wceq ppncant mp3an
      2times eqtr4 opreq2i mulass 2t2e4 opreq1i 3eqtr2r pnncan normlem8
      normlem9 opreq12i 3eqtr4 cneg axicn hvmulcl ccj cfv chil his5t cji eqtr
      ax-his3 eqtr3 3eqtr negcl mulcl mul12 c1 mulneg2 ixi negeqi ax1cn negneg
      mulid2 3eqtr3 mulm1 negsub 3eqtr2 ) IABJKZLKZIMKWNACNKDBNKJKZACOKDBOKJKZP
      KZQAQCRKZNKDQBRKZNKJKZAWSOKDWTOKJKZPKZLKZSKZIMKIWNITUAABEFUBZITUCUDUEWOXE
      IMUFWNCDJKZSKZWNXGPKZSKZLKZUFXHLKZUFXILKZSKWOXEUFXHXIUGWNXGXFCDGHUBZUHZWN
      XGXFXNUIUJXKUFUFWNLKZLKUFUFLKZWNLKWOXJXPUFLXJWNWNSKZXPWNUKULZXGUKULXSXJXR
      UMXFXNXFWNXGWNUNUOWNXFUPUQURUFUFWNUGUGXFUSXQIWNLUTVAVBWRXLXDXMSADJKZCBJKZ
      SKZXHSKZYBXHPKZPKXHXHSKWRXLYBXHXHXTYAADEHUBZCBGFUBUHXOXOVCWPYCWQYDPACDBEG
      HFVDACDBEGHFVEVFXHXOUPVGXDQUFQVHZWNLKZQXGLKZSKZLKZLKUFQYILKZLKXMXCYJQLXCX
      TWSWTJKZSKZAWTJKZWSDJKZSKZSKZYMYPPKZPKYPYPSKZYJXAYQXBYRPAWSDWTEQCVIGVJZHQ
      BVIFVJZVDAWSDWTEYTHUUAVEVFYMYPYPXTYLYEWSWTYTUUAUBUHYNYOAWTEUUAUBWSDYTHUBU
      HZUUBVCUFYPLKYSYJYPUUBUPYPYIUFLYNYGYOYHSYNQVKVLZWNLKZYGQUKULZAVMULBVMULYN
      UUDUMVIEFQABVNUOUUCYFWNLVOVAVPUUECVMULDVMULYOYHUMVIGHQCDVQUOVFURVRVSURUFQ
      YIUGVIYGYHYFWNQVIVTZXFWAZQXGVIXNWAZUHWBYKXIUFLYKQYGLKZQYHLKZSKWNXGVHZSKXI
      QYGYHVIUUGUUHUJUUIWNUUJUUKSQYFLKZWNLKWCWNLKUUIWNUULWCWNLUULQQLKZVHWCVHZVH
      WCQQVIVIWDUUMUUNWEWFWCWGWHVSVAQYFWNVIUUFXFUSWNXFWIWJUUMXGLKUUNXGLKUUJUUKU
      UMUUNXGLWEVAQQXGVIVIXNUSXGXNWKWJVFWNXGXFXNWLVSURWMVFVGVAVR $.
      $( [11-Jul-2005] $) $( [30-Jun-2005] $)
  $}

  ${
    polid.1 $e |- A e. H~ $.
    polid.2 $e |- B e. H~ $.
    $( Polarization identity.  Recovers inner product from norm.  Exercise 4(a)
       of [ReedSimon] p. 63.  The outermost operation is + instead of - due to
       our mathematicians' (rather than physicists') version of axiom
       ~ ax-his3 . $)
    polid $p |- ( A .ih B ) = ( ( ( ( ( normh ` ( A +h B ) ) ^ 2 ) -
                ( ( normh ` ( A -h B ) ) ^ 2 ) ) +
       ( i x. ( ( ( normh ` ( A +h ( i .h B ) ) ) ^ 2 ) -
                ( ( normh ` ( A -h ( i .h B ) ) ) ^ 2 ) ) ) ) / 4 ) $=
      ( csp co cva cmv cmin ci csm cmul caddc c4 cdiv cno cfv c2 cexp polid2
      hvaddcl normsq hvsubcl opreq12i axicn hvmulcl opreq2i opreq1i eqtr4 ) ABE
      FABGFZUJEFZABHFZULEFZIFZJAJBKFZGFZUPEFZAUOHFZUREFZIFZLFZMFZNOFUJPQRSFZULP
      QRSFZIFZJUPPQRSFZURPQRSFZIFZLFZMFZNOFABBACDDCTVJVBNOVEUNVIVAMVCUKVDUMIUJA
      BCDUAUBULABCDUCUBUDVHUTJLVFUQVGUSIUPAUOCJBUEDUFZUAUBURAUOCVKUCUBUDUGUDUHU
      I $.
      $( [20-Jul-2005] $) $( [30-Jun-2005] $)
  $}

  $( Polarization identity.  Recovers inner product from norm.  Exercise 4(a)
     of [ReedSimon] p. 63.  The outermost operation is + instead of - due to
     our mathematicians' (rather than physicists') version of axiom
     ~ ax-his3 . $)
  polidt $p |- ( ( A e. H~ /\ B e. H~ ) ->
           ( A .ih B ) = ( ( ( ( ( normh ` ( A +h B ) ) ^ 2 ) -
              ( ( normh ` ( A -h B ) ) ^ 2 ) ) +
       ( i x. ( ( ( normh ` ( A +h ( i .h B ) ) ) ^ 2 ) -
                ( ( normh ` ( A -h ( i .h B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
    ( chil wcel csp co cva cno cfv c2 cexp cmv cmin ci csm cmul caddc c4 cdiv
    wceq c0v cif opreq1 fveq2d opreq1d opreq12d opreq2d eqeq12d opreq2
    ax-hv0cl elimel polid dedth2h ) ACDZBCDZABEFZABGFZHIZJKFZABLFZHIZJKFZMFZNAN
    BOFZGFZHIZJKFZAVDLFZHIZJKFZMFZPFZQFZRSFZTUNAUAUBZBEFZVOBGFZHIZJKFZVOBLFZHIZ
    JKFZMFZNVOVDGFZHIZJKFZVOVDLFZHIZJKFZMFZPFZQFZRSFZTVOUOBUAUBZEFZVOWNGFZHIZJK
    FZVOWNLFZHIZJKFZMFZNVONWNOFZGFZHIZJKFZVOXCLFZHIZJKFZMFZPFZQFZRSFZTABUAUAAVO
    TZUPVPVNWMAVOBEUCXNVMWLRSXNVCWCVLWKQXNUSVSVBWBMXNURVRJKXNUQVQHAVOBGUCUDUEXN
    VAWAJKXNUTVTHAVOBLUCUDUEUFXNVKWJNPXNVGWFVJWIMXNVFWEJKXNVEWDHAVOVDGUCUDUEXNV
    IWHJKXNVHWGHAVOVDLUCUDUEUFUGUFUEUHBWNTZVPWOWMXMBWNVOEUIXOWLXLRSXOWCXBWKXKQX
    OVSWRWBXAMXOVRWQJKXOVQWPHBWNVOGUIUDUEXOWAWTJKXOVTWSHBWNVOLUIUDUEUFXOWJXJNPX
    OWFXFWIXIMXOWEXEJKXOWDXDHXOVDXCVOGBWNNOUIZUGUDUEXOWHXHJKXOWGXGHXOVDXCVOLXPU
    GUDUEUFUGUFUEUHVOWNAUACUJUKBUACUJUKULUM $.
    $( [24-Dec-2007] $) $( [17-Nov-2007] $)

