$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Norms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Alternate definition of the norm of a vector of Hilbert space.
       Definition of norm in [Beran] p. 96.  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    dfhnorm2 $p |- normh = ( x e. ~H |-> ( sqr ` ( x .ih x ) ) ) $=
      ( cno csp cdm cv co csqr cfv cmpt chil df-hnorm cxp cc ax-hfi fdmi dmxpid
      dmeqi eqtr2i eqid mpteq12i eqtr4i ) BACDZDZAEZUDCFGHZIAJUEIAKAJUEUCUEUCJJ
      LZDJUBUFUFMCNOQJPRUESTUA $.
      $( [15-Dec-2013] $) $( [6-Jun-2008] $)

    $( The norm function maps from Hilbert space to reals. $)
    normf $p |- normh : ~H --> RR $=
      ( vx cv csp co csqr cfv cr wcel chil wral cno cc0 cle wbr hiidrcl hiidge0
      wf resqrcl syl11anc rgen dfhnorm2 fmpt mpbi ) ABZUDCDZEFZGHZAIJIGKQUGAIUD
      IHUEGHLUEMNUGUDOUDPUERSTAIGUFKAUAUBUC $.
      $( [15-Dec-2013] $) $( [6-Sep-2007] $)
  $}

  ${
    $d x A $.
    $( The value of the norm of a vector in Hilbert space.  Definition of norm
       in [Beran] p. 96.  In the literature, the norm of ` A ` is usually
       written as "|| ` A ` ||", but we use function value notation to take
       advantage of our existing theorems about functions. $)
    normval $p |- ( A e. ~H -> ( normh ` A ) = ( sqr ` ( A .ih A ) ) ) $=
      ( vx cv csp co csqr cfv chil cno wceq opreq12 anidms fveq2d dfhnorm2 fvex
      fvmpt ) BABCZQDEZFGAADEZFGHIQAJZRSFTRSJQAQADKLMBNSFOP $.
      $( [23-Dec-2013] $) $( [29-May-1999] $)
  $}

  $( Real closure of the norm of a vector. $)
  normcl $p |- ( A e. ~H -> ( normh ` A ) e. RR ) $=
    ( chil cr cno normf ffvelrni ) BCADEF $.
    $( [29-May-1999] $)

  $( The norm of a vector is nonnegative. $)
  normge0 $p |- ( A e. ~H -> 0 <_ ( normh ` A ) ) $=
    ( chil wcel cc0 csp co csqr cfv cno cle wbr hiidrcl hiidge0 sqrge0 syl11anc
    cr normval breqtrrd ) ABCZDAAEFZGHZAIHJSTPCDTJKDUAJKALAMTNOAQR $.
    $( [29-May-1999] $)

  $( The norm of nonzero vector is positive. $)
  normgt0 $p |- ( A e. ~H -> ( A =/= 0h <-> 0 < ( normh ` A ) ) ) $=
    ( chil wcel c0v wne cc0 csp co csqr cfv clt wbr wa cr hiidrcl syl11anc wceq
    cno ex wn adantr ax-his4 sqrgt0 opreq1 hi01 sylan9eqr fveq2d sqr0 syl6eq wb
    cle hiidge0 resqrcl 0re jctir lttri3 syl simpr syl6bi syld necon2ad normval
    impbid breq2d bitr4d ) ABCZADEZFAAGHZIJZKLZFARJZKLVFVGVJVFVGVJVFVGMVHNCZFVH
    KLVJVFVLVGAOZUAAUBVHUCPSVFVJADVFADQZVIFQZVJTZVFVNVOVFVNMZVIFIJFVQVHFIVNVFVH
    DAGHFADAGUDAUEUFUGUHUISVFVOVIFKLTZVPMZVPVFVINCZFNCZMVOVSUJVFVTWAVFVLFVHUKLV
    TVMAULVHUMPUNUOVIFUPUQVRVPURUSUTVAVCVFVKVIFKAVBVDVE $.
    $( [10-Apr-2006] $)

  $( The norm of a zero vector. $)
  norm0 $p |- ( normh ` 0h ) = 0 $=
    ( c0v cno cfv csp co csqr chil wcel wceq ax-hv0cl normval ax-mp hi01 fveq2d
    cc0 sqr0 3eqtri ) ABCZAADEZFCZOFCZOAGHZRTIJAKLUBTUAIJUBSOFAMNLPQ $.
    $( [30-May-1999] $)

  $( Theorem 3.3(i) of [Beran] p. 97. $)
  norm-i $p |- ( A e. ~H -> ( ( normh ` A ) = 0 <-> A = 0h ) ) $=
    ( chil wcel c0v wceq cno cfv cc0 wne clt wbr normgt0 cle normcl normge0 0re
    cr wb leltne mp3an1 syl11anc bitrd necon4bid bicomd ) ABCZADEAFGZHEUEADUFHU
    EADIHUFJKZUFHIZALUEUFQCZHUFMKZUGUHRZANAOHQCUIUJUKPHUFSTUAUBUCUD $.
    $( [29-Jul-1999] $)

  $( A norm is nonzero iff its argument is a nonzero vector. $)
  normne0 $p |- ( A e. ~H -> ( ( normh ` A ) =/= 0 <-> A =/= 0h ) ) $=
    ( chil wcel cno cfv cc0 c0v norm-i necon3bid ) ABCADEFAGAHI $.
    $( [11-Mar-2006] $)

  ${
    normcl.1 $e |- A e. ~H $.
    $( Real closure of the norm of a vector. $)
    normcli $p |- ( normh ` A ) e. RR $=
      ( chil wcel cno cfv cr normcl ax-mp ) ACDAEFGDBAHI $.
      $( [30-Sep-1999] $)

    $( The square of a norm. $)
    normsqi $p |- ( ( normh ` A ) ^ 2 ) = ( A .ih A ) $=
      ( cno cfv c2 cexp co csp csqr chil wcel normval ax-mp opreq1i cc0 cle wbr
      wceq hiidge0 cr hiidrcl sqsqri eqtri ) ACDZEFGAAHGZIDZEFGZUEUDUFEFAJKZUDU
      FRBALMNOUEPQZUGUERUHUIBASMUEUHUETKBAUAMUBMUC $.
      $( [21-Aug-1999] $)

    $( Theorem 3.3(i) of [Beran] p. 97. $)
    norm-i-i $p |- ( ( normh ` A ) = 0 <-> A = 0h ) $=
      ( chil wcel cno cfv cc0 wceq c0v wb norm-i ax-mp ) ACDAEFGHAIHJBAKL $.
      $( [5-Sep-1999] $)
  $}

  $( The square of a norm. $)
  normsq $p |- ( A e. ~H -> ( ( normh ` A ) ^ 2 ) = ( A .ih A ) ) $=
    ( chil wcel cno cfv c2 cexp csp wceq c0v cif fveq2 opreq1d opreq12d eqeq12d
    co id ax-hv0cl elimel normsqi dedth ) ABCZADEZFGPZAAHPZIUBAJKZDEZFGPZUFUFHP
    ZIAJAUFIZUDUHUEUIUJUCUGFGAUFDLMUJAUFAUFHUJQZUKNOUFAJBRSTUA $.
    $( [12-May-2005] $)

  ${
    normsub0.1 $e |- A e. ~H $.
    normsub0.2 $e |- B e. ~H $.
    $( Two vectors are equal iff the norm of their difference is zero. $)
    normsub0i $p |- ( ( normh ` ( A -h B ) ) = 0 <-> A = B ) $=
      ( cmv co cno cfv cc0 wceq c0v hvsubcli norm-i-i hvsubeq0i bitri ) ABEFZGH
      IJPKJABJPABCDLMABCDNO $.
      $( [18-Aug-1999] $)
  $}

  $( Two vectors are equal iff the norm of their difference is zero. $)
  normsub0 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( normh ` ( A -h B ) ) = 0 <-> A = B ) ) $=
    ( chil wcel cmv co cno cfv cc0 wceq wb c0v cif opreq1 fveq2d eqeq1d bibi12d
    eqeq1 ax-hv0cl elimel opreq2 eqeq2 normsub0i dedth2h ) ACDZBCDZABEFZGHZIJZA
    BJZKUEALMZBEFZGHZIJZUKBJZKUKUFBLMZEFZGHZIJZUKUPJZKABLLAUKJZUIUNUJUOVAUHUMIV
    AUGULGAUKBENOPAUKBRQBUPJZUNUSUOUTVBUMURIVBULUQGBUPUKEUAOPBUPUKUBQUKUPALCSTB
    LCSTUCUD $.
    $( [18-Aug-1999] $)

  ${
    norm-ii.1 $e |- A e. ~H $.
    norm-ii.2 $e |- B e. ~H $.
    $( Triangle inequality for norms.  Theorem 3.3(ii) of [Beran] p. 97. $)
    norm-ii-i $p |- ( normh ` ( A +h B ) ) <_
                  ( ( normh ` A ) + ( normh ` B ) ) $=
      ( co csp csqr cfv caddc cle c2 wbr cmul c1 cr wcel wceq ax-1cn cc0 ax-mp
      cva cno cexp ccj 1re cjrebi mpbi opreq1i hicli mulid2i eqtri opreq12i 0re
      cabs lt01 ltleii absidi normlem7 eqbrtrri cneg eqid normlem2 cjcli mulcli
      addcli negrebi eqeltrri 2re chil hiidge0 hiidrcl sqrcli remulcli readdcli
      leadd2i normlem8 addcomi opreq2i recni binom2i sqcli add32i sqsqri 3eqtri
      2cn 3brtr4i hvaddcli sqge0i resqcli sqrlei sqrge0i addge0i sqrsqi breqtri
      wb mp2an normval ) ABUAEZWRFEZGHZAAFEZGHZBBFEZGHZIEZWRUBHZAUBHZBUBHZIEJWT
      XEKUCEZGHZXEJWSXIJLZWTXJJLZXAXCIEZBAFEZABFEZIEZIEZXMKXBXDMEZMEZIEZWSXIJXP
      XSJLXQXTJLNUDHZXNMEZNXOMEZIEZXPXSJYBXNYCXOIYBNXNMEXNYANXNMNOPYANQUENRUFUG
      UHXNBADCUIZUJUKXOABCDUIZUJULZNBARDCSNJLNUNHNQSNUMUEUOUPNUEUQTURUSXPXSXMYD
      XPOYGYDUTZOPYDOPYHNBARDCYHVAVBYDYBYCYAXNNRVCYEVDNXORYFVDVEVFUGVGKXRVHXBXD
      SXAJLZXBOPAVIPZYICAVJTZXAYJXAOPCAVKTZVLTZSXCJLZXDOPBVIPZYNDBVJTZXCYOXCOPD
      BVKTZVLTZVMVMXAXCYLYQVNVOUGWSXMXOXNIEZIEXQABABCDCDVPYSXPXMIXOXNYFYEVQVRUK
      XIXBKUCEZXSIEXDKUCEZIEYTUUAIEZXSIEXTXBXDXBYMVSZXDYRVSZVTYTXSUUAXBUUCWAKXR
      WEXBXDUUCUUDVDVDXDUUDWAWBUUBXMXSIYTXAUUAXCIYIYTXAQYKXAYLWCTYNUUAXCQYPXCYQ
      WCTULUHWDWFSWSJLZSXIJLXKXLWOWRVIPZUUEABCDWGZWRVJTXEXBXDYMYRVNZWHWSXIUUFWS
      OPUUGWRVKTXEUUHWIWJWPUGSXEJLZXJXEQSXBJLZSXDJLZUUIYIUUJYKXAYLWKTYNUUKYPXCY
      QWKTXBXDYMYRWLWPXEUUHWMTWNUUFXFWTQUUGWRWQTXGXBXHXDIYJXGXBQCAWQTYOXHXDQDBW
      QTULWF $.
      $( [11-Aug-1999] $)
  $}

  $( Triangle inequality for norms.  Theorem 3.3(ii) of [Beran] p. 97. $)
  norm-ii $p |- ( ( A e. ~H /\ B e. ~H ) ->
               ( normh ` ( A +h B ) ) <_ ( ( normh ` A ) + ( normh ` B ) ) ) $=
    ( chil wcel cva co cno cfv caddc cle wbr c0v cif wceq opreq1 fveq2d breq12d
    fveq2 ax-hv0cl elimel opreq1d opreq2 opreq2d norm-ii-i dedth2h ) ACDZBCDZAB
    EFZGHZAGHZBGHZIFZJKUFALMZBEFZGHZUMGHZUKIFZJKUMUGBLMZEFZGHZUPURGHZIFZJKABLLA
    UMNZUIUOULUQJVCUHUNGAUMBEOPVCUJUPUKIAUMGRUAQBURNZUOUTUQVBJVDUNUSGBURUMEUBPV
    DUKVAUPIBURGRUCQUMURALCSTBLCSTUDUE $.
    $( [10-Mar-2006] $)

  ${
    norm-iii.1 $e |- A e. CC $.
    norm-iii.2 $e |- B e. ~H $.
    $( Theorem 3.3(iii) of [Beran] p. 97. $)
    norm-iii-i $p |- ( normh ` ( A .h B ) )
         = ( ( abs ` A ) x. ( normh ` B ) ) $=
      ( csm co csp csqr cfv ccj cmul cabs his35i fveq2i chil wcel ax-mp normval
      cno wceq cjmulrcli cr hiidrcl cjmulge0i cc0 cle hiidge0 sqrmulii hvmulcli
      wbr eqtri cc absval opreq12i 3eqtr4i ) ABEFZUPGFZHIZAAJIKFZHIZBBGFZHIZKFZ
      UPSIZALIZBSIZKFURUSVAKFZHIVCUQVGHAABBCCDDMNUSVAACUABOPZVAUBPDBUCQACUDVHUE
      VAUFUJDBUGQUHUKUPOPVDURTABCDUIUPRQVEUTVFVBKAULPVEUTTCAUMQVHVFVBTDBRQUNUO
      $.
      $( [29-Jul-1999] $)
  $}

  $( Theorem 3.3(iii) of [Beran] p. 97. $)
  norm-iii $p |- ( ( A e. CC /\ B e. ~H )
             -> ( normh ` ( A .h B ) ) = ( ( abs ` A ) x. ( normh ` B ) ) ) $=
    ( cc wcel chil csm cno cfv cabs cmul wceq cc0 cif c0v opreq1 fveq2d eqeq12d
    co fveq2 elimel opreq1d opreq2 opreq2d 0cn ax-hv0cl norm-iii-i dedth2h ) AC
    DZBEDZABFRZGHZAIHZBGHZJRZKUHALMZBFRZGHZUOIHZUMJRZKUOUIBNMZFRZGHZURUTGHZJRZK
    ABLNAUOKZUKUQUNUSVEUJUPGAUOBFOPVEULURUMJAUOISUAQBUTKZUQVBUSVDVFUPVAGBUTUOFU
    BPVFUMVCURJBUTGSUCQUOUTALCUDTBNEUETUFUG $.
    $( [25-Oct-1999] $)

  ${
    normsub.1 $e |- A e. ~H $.
    normsub.2 $e |- B e. ~H $.
    $( Negative doesn't change the norm of a Hilbert space vector. $)
    normsubi $p |- ( normh ` ( A -h B ) ) = ( normh ` ( B -h A ) ) $=
      ( cmv co cno cfv c1 cneg cabs cmul hvnegdii fveq2i ax-1cn negcli hvsubcli
      csm cc0 1re norm-iii-i eqtr3i absnegi cle wbr wceq 0re lt01 ltleii absidi
      ax-mp eqtri opreq1i normcli recni mulid2i 3eqtri ) ABEFZGHZIJZKHZBAEFZGHZ
      LFZIVCLFVCUTVBRFZGHUSVDVEURGBADCMNUTVBIOPBADCQZUAUBVAIVCLVAIKHZIIOUCSIUDU
      EVGIUFSIUGTUHUIITUJUKULUMVCVCVBVFUNUOUPUQ $.
      $( [11-Aug-1999] $)

    $( Analogy to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
       [Beran] p. 98. $)
    normpythi $p |- ( ( A .ih B ) = 0 -> ( ( normh ` ( A +h B ) ) ^ 2 ) =
                   ( ( ( normh ` A ) ^ 2 ) + ( ( normh ` B ) ^ 2 ) ) ) $=
      ( csp co cc0 wceq cva caddc cno cfv cexp chil wcel addid1i syl6eq normsqi
      c2 hicli normlem8 id wb orthcom biimpi opreq12d 0cn opreq2d addcli syl5eq
      mp2an hvaddcli opreq12i 3eqtr4g ) ABEFZGHZABIFZUQEFZAAEFZBBEFZJFZUQKLSMFA
      KLSMFZBKLSMFZJFUPURVAUOBAEFZJFZJFZVAABABCDCDUAUPVFVAGJFVAUPVEGVAJUPVEGGJF
      GUPUOGVDGJUPUBUPVDGHZANOBNOUPVGUCCDABUDUKUEUFGUGPQUHVAUSUTAACCTBBDDTUIPQU
      JUQABCDULRVBUSVCUTJACRBDRUMUN $.
      $( [17-Oct-1999] $)
  $}

  $( Swapping order of subtraction doesn't change the norm of a vector. $)
  normsub $p |- ( ( A e. ~H /\ B e. ~H ) ->
                 ( normh ` ( A -h B ) ) = ( normh ` ( B -h A ) ) ) $=
    ( chil wcel cmv co cno cfv wceq c0v cif opreq1 fveq2d opreq2 eqeq12d elimel
    ax-hv0cl normsubi dedth2h ) ACDZBCDZABEFZGHZBAEFZGHZITAJKZBEFZGHZBUFEFZGHZI
    UFUABJKZEFZGHZUKUFEFZGHZIABJJAUFIZUCUHUEUJUPUBUGGAUFBELMUPUDUIGAUFBENMOBUKI
    ZUHUMUJUOUQUGULGBUKUFENMUQUIUNGBUKUFELMOUFUKAJCQPBJCQPRS $.
    $( [14-Aug-1999] $)

  $( The norm of a vector equals the norm of its negative. $)
  normneg $p |- ( A e. ~H ->
                 ( normh ` ( -u 1 .h A ) ) = ( normh ` A ) ) $=
    ( chil wcel c0v cmv co cno cfv cneg csm wceq ax-hv0cl normsub hv2neg fveq2d
    c1 mpan hvsub0 3eqtr3d ) ABCZDAEFZGHZADEFZGHZPIAJFZGHAGHDBCTUBUDKLDAMQTUAUE
    GANOTUCAGAROS $.
    $( [23-May-2005] $)

  $( Analogy to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
     [Beran] p. 98. $)
  normpyth $p |- ( ( A e. ~H /\ B e. ~H ) ->
                 ( ( A .ih B ) = 0 -> ( ( normh ` ( A +h B ) ) ^ 2 ) =
                 ( ( ( normh ` A ) ^ 2 ) + ( ( normh ` B ) ^ 2 ) ) ) ) $=
    ( chil wcel csp co cc0 wceq cva cno cfv c2 cexp caddc c0v cif opreq1 eqeq1d
    wi opreq1d fveq2d eqeq12d imbi12d opreq2 opreq2d ax-hv0cl normpythi dedth2h
    fveq2 elimel ) ACDZBCDZABEFZGHZABIFZJKZLMFZAJKZLMFZBJKZLMFZNFZHZSUKAOPZBEFZ
    GHZVDBIFZJKZLMFZVDJKZLMFZVANFZHZSVDULBOPZEFZGHZVDVNIFZJKZLMFZVKVNJKZLMFZNFZ
    HZSABOOAVDHZUNVFVCVMWDUMVEGAVDBEQRWDUQVIVBVLWDUPVHLMWDUOVGJAVDBIQUATWDUSVKV
    ANWDURVJLMAVDJUITTUBUCBVNHZVFVPVMWCWEVEVOGBVNVDEUDRWEVIVSVLWBWEVHVRLMWEVGVQ
    JBVNVDIUDUATWEVAWAVKNWEUTVTLMBVNJUITUEUBUCVDVNAOCUFUJBOCUFUJUGUH $.
    $( [17-Oct-1999] $)

  $( Corollary to Pythagorean theorem for orthogonal vectors.  Remark 3.4(C) of
     [Beran] p. 98. $)
  normpyc $p |- ( ( A e. ~H /\ B e. ~H ) ->
            ( ( A .ih B ) = 0 -> ( normh ` A ) <_ ( normh ` ( A +h B ) ) ) ) $=
    ( chil wcel wa co cc0 wceq cno cfv c2 cle wbr caddc cr normcl resqcl adantr
    cexp syl csp cva cc recnd addid1 sqge0 adantl wb leadd2 mp3an1 ancoms mpbid
    0re syl2an eqbrtrrd normpyth imp breqtrrd ex normge0 hvaddcl le2sq syl22anc
    sylibrd ) ACDZBCDZEZABUAFGHZAIJZKSFZABUBFZIJZKSFZLMZVIVLLMZVGVHVNVGVHEVJVJB
    IJZKSFZNFZVMLVGVJVRLMVHVGVJGNFZVJVRLVEVSVJHZVFVEVJUCDVTVEVJVEVIODZVJODZAPZV
    IQTZUDVJUETRVGGVQLMZVSVRLMZVFWEVEVFVPODZWEBPZVPUFTUGVEWBVQODZWEWFUHZVFWDVFW
    GWIWHVPQTWIWBWJGODWIWBWJUMGVQVJUIUJUKUNULUORVGVHVMVRHABUPUQURUSVGWAGVILMZVL
    ODZGVLLMZVOVNUHVEWAVFWCRVEWKVFAUTRVGVKCDZWLABVAZVKPTVGWNWMWOVKUTTVIVLVBVCVD
    $.
    $( [26-Oct-1999] $)

  ${
    norm3dif.1 $e |- A e. ~H $.
    norm3dif.2 $e |- B e. ~H $.
    norm3dif.3 $e |- C e. ~H $.
    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3difi $p |- ( normh ` ( A -h B ) ) <_ ( ( normh ` ( A -h C ) ) +
                   ( normh ` ( C -h B ) ) ) $=
      ( cmv co cno cfv cva csm hvmulcli hvassi c0v hvcomi hvsubvali chil wcel
      c1 caddc cle cneg ax-1cn negcli hvaddcli hvsubid 3eqtr2i opreq1i ax-hv0cl
      wceq ax-mp 3eqtri eqtr3i opreq2i eqtr2i opreq12i 3eqtr4i fveq2i norm-ii-i
      ax-hvaddid hvsubcli eqbrtri ) ABGHZIJACGHZCBGHZKHZIJVEIJVFIJUAHUBVDVGIATU
      CZBLHZKHZAVHCLHZKHZCVIKHZKHZVDVGVNAVKVMKHZKHVJAVKVMDVHCTUDUEZFMZCVIFVHBVP
      EMZUFNVOVIAKVKCKHZVIKHZVOVIVKCVIVQFVRNVTOVIKHVIOKHZVIVSOVIKVSCVKKHCCGHZOV
      KCVQFPCCFFQCRSWBOUKFCUGULUHUIOVIUJVRPVIRSWAVIUKVRVIVAULUMUNUOUPABDEQVEVLV
      FVMKACDFQCBFEQUQURUSVEVFACDFVBCBFEVBUTVC $.
      $( [16-Aug-1999] $)

    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3adifii $p |- ( abs ` ( ( normh ` ( A -h C ) ) -
         ( normh ` ( B -h C ) ) ) ) <_ ( normh ` ( A -h B ) ) $=
      ( cmv co cno cfv cmin cle wbr cneg hvsubcli normcli recni caddc norm3difi
      lesubaddi cabs resubcli negsubdi2i normsubi opreq1i breqtri mpbir eqbrtri
      abslei lenegcon1i mpbi mpbir2an ) ACGHZIJZBCGHZIJZKHZUAJABGHZIJZLMUSNUQLM
      ZUQUSLMZUQUSUNUPUMACDFOPZUOBCEFOPZUBZURABDEOPZUIUQNZUSLMUTVFUPUNKHZUSLUNU
      PUNVBQUPVCQUCVGUSLMUPUSUNRHZLMUPBAGHIJZUNRHVHLBCAEFDSVIUSUNRBAEDUDUEUFUPU
      NUSVCVBVETUGUHUQUSVDVEUJUKVAUNUSUPRHLMACBDFESUNUPUSVBVCVETUGUL $.
      $( [30-Sep-1999] $)

    ${
      norm3lem.4 $e |- D e. RR $.
      $( Lemma involving norm of differences in Hilbert space. $)
      norm3lem $p |- ( ( ( normh ` ( A -h C ) ) < ( D / 2 ) /\
                         ( normh ` ( C -h B ) ) < ( D / 2 ) ) ->
                         ( normh ` ( A -h B ) ) < D ) $=
        ( cmv co cno cfv c2 clt wbr caddc hvsubcli normcli 2ne0 readdcli wa 2re
        cdiv redivcli lt2addi cle norm3difi lelttri mpan syl cmul recni 2timesi
        2cn divcan2i eqtr3i syl6breq ) ACIJZKLZDMUCJZNOCBIJZKLZUTNOUAZABIJZKLZU
        TUTPJZDNVCUSVBPJZVFNOZVEVFNOZUSVBUTUTURACEGQRZVACBGFQRZDMHUBSUDZVLUEVEV
        GUFOVHVIABCEFGUGVEVGVFVDABEFQRUSVBVJVKTUTUTVLVLTUHUIUJMUTUKJVFDUTUTVLUL
        UMDMDHULUNSUOUPUQ $.
        $( [18-Aug-1999] $)
    $}
  $}

  $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
     p. 101. $)
  norm3dif $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) -> ( normh ` ( A -h B ) )
                  <_ ( ( normh ` ( A -h C ) ) + ( normh ` ( C -h B ) ) ) ) $=
    ( chil wcel cmv cno cfv caddc cle wbr c0v cif opreq1 fveq2d opreq2 ax-hv0cl
    co wceq elimel opreq1d breq12d opreq2d opreq12d breq2d norm3difi dedth3h )
    ADEZBDEZCDEZABFRZGHZACFRZGHZCBFRZGHZIRZJKUHALMZBFRZGHZURCFRZGHZUPIRZJKURUIB
    LMZFRZGHZVBCVDFRZGHZIRZJKVFURUJCLMZFRZGHZVJVDFRZGHZIRZJKABCLLLAURSZULUTUQVC
    JVPUKUSGAURBFNOVPUNVBUPIVPUMVAGAURCFNOUAUBBVDSZUTVFVCVIJVQUSVEGBVDURFPOVQUP
    VHVBIVQUOVGGBVDCFPOUCUBCVJSZVIVOVFJVRVBVLVHVNIVRVAVKGCVJURFPOVRVGVMGCVJVDFN
    OUDUEURVDVJALDQTBLDQTCLDQTUFUG $.
    $( [20-Apr-2006] $)

  $( Norm of differences around common element. $)
  norm3dif2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) -> ( normh ` ( A -h B )
                  ) <_ ( ( normh ` ( C -h A ) ) + ( normh ` ( C -h B ) ) ) ) $=
    ( chil wcel w3a cmv cno cfv caddc cle norm3dif wceq normsub 3adant2 opreq1d
    co breqtrd ) ADEZBDEZCDEZFZABGQHIACGQHIZCBGQHIZJQCAGQHIZUDJQKABCLUBUCUEUDJS
    UAUCUEMTACNOPR $.
    $( [18-Apr-2007] $)

  $( Lemma involving norm of differences in Hilbert space. $)
  norm3lemt $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\
                   D e. RR ) ) -> (
          ( ( normh ` ( A -h C ) ) < ( D / 2 ) /\
            ( normh ` ( C -h B ) ) < ( D / 2 ) ) ->
            ( normh ` ( A -h B ) ) < D ) ) $=
    ( chil wcel cmv co cno cfv c2 clt wbr wa wi c0v cif wceq fveq2d breq1d cdiv
    cr opreq1 anbi1d imbi12d opreq2 anbi2d anbi12d imbi1d breq2d breq2 ax-hv0cl
    elimel 2re norm3lem dedth4h ) AEFZBEFZCEFZDUBFZACGHZIJZDKUAHZLMZCBGHZIJZVCL
    MZNZABGHZIJZDLMZOUQAPQZCGHZIJZVCLMZVGNZVLBGHZIJZDLMZOVOCURBPQZGHZIJZVCLMZNZ
    VLVTGHZIJZDLMZOVLUSCPQZGHZIJZVCLMZWHVTGHZIJZVCLMZNZWGOWJUTDKQZKUAHZLMZWMWQL
    MZNZWFWPLMZOABCDPPPKAVLRZVHVPVKVSXBVDVOVGXBVBVNVCLXBVAVMIAVLCGUCSTUDXBVJVRD
    LXBVIVQIAVLBGUCSTUEBVTRZVPWDVSWGXCVGWCVOXCVFWBVCLXCVEWAIBVTCGUFSTUGXCVRWFDL
    XCVQWEIBVTVLGUFSTUECWHRZWDWOWGXDVOWKWCWNXDVNWJVCLXDVMWIICWHVLGUFSTXDWBWMVCL
    XDWAWLICWHVTGUCSTUHUIDWPRZWOWTWGXAXEWKWRWNWSXEVCWQWJLDWPKUAUCZUJXEVCWQWMLXF
    UJUHDWPWFLUKUEVLVTWHWPAPEULUMBPEULUMCPEULUMDKUBUNUMUOUP $.
    $( [18-Aug-1999] $)

  ${
    norm3adift.1 $e |- C e. ~H $.
    $( Norm of differences around common element.  Part of Lemma 3.6 of [Beran]
       p. 101. $)
    norm3adifi $p |- ( ( A e. ~H /\ B e. ~H ) ->
         ( abs ` ( ( normh ` ( A -h C ) ) -
         ( normh ` ( B -h C ) ) ) ) <_ ( normh ` ( A -h B ) ) ) $=
      ( chil wcel cmv co cno cfv cmin cabs cle wbr c0v cif wceq opreq1 breq12d
      fveq2d opreq1d opreq2d opreq2 ax-hv0cl elimel norm3adifii dedth2h ) AEFZB
      EFZACGHZIJZBCGHZIJZKHZLJZABGHZIJZMNUHAOPZCGHZIJZUMKHZLJZURBGHZIJZMNUTUIBO
      PZCGHZIJZKHZLJZURVEGHZIJZMNABOOAURQZUOVBUQVDMVLUNVALVLUKUTUMKVLUJUSIAURCG
      RTUATVLUPVCIAURBGRTSBVEQZVBVIVDVKMVMVAVHLVMUMVGUTKVMULVFIBVECGRTUBTVMVCVJ
      IBVEURGUCTSURVECAOEUDUEBOEUDUEDUFUG $.
      $( [3-Oct-1999] $)
  $}

  ${
    normpar.1 $e |- A e. ~H $.
    normpar.2 $e |- B e. ~H $.
    $( Parallelogram law for norms.  Remark 3.4(B) of [Beran] p. 98. $)
    normpari $p |- ( ( ( normh ` ( A -h B ) ) ^ 2 ) +
                    ( ( normh ` ( A +h B ) ) ^ 2 ) ) =
                  ( ( 2 x. ( ( normh ` A ) ^ 2 ) ) +
                    ( 2 x. ( ( normh ` B ) ^ 2 ) ) ) $=
      ( co csp caddc cno cfv cexp cmul hicli addcli opreq12i cc0 opreq2i 3eqtri
      c2 normsqi 2timesi cmv cva normlem9 negsubi eqtr4i normlem8 negcli add42i
      cneg cmin negidi addid1i add4i hvsubcli hvaddcli eqtri 3eqtr4i ) ABUAEZUR
      FEZABUBEZUTFEZGEZAAFEZVCGEZBBFEZVEGEZGEZURHIRJEZUTHIRJEZGERAHIRJEZKEZRBHI
      RJEZKEZGEVBVCVEGEZABFEZBAFEZGEZUIZGEZVNVQGEZGEVNVNGEZVQVRGEZGEZVGUSVSVAVT
      GUSVNVQUJEVSABABCDCDUCVNVQVCVEAACCLZBBDDLZMZVOVPABCDLBADCLMZUDUEABABCDCDU
      FNVNVRVNVQWFVQWGUGWFWGUHWCWAOGEWAVGWBOWAGVQWGUKPWAVNVNWFWFMULVCVEVCVEWDWE
      WDWEUMQQVHUSVIVAGURABCDUNSUTABCDUOSNVKVDVMVFGVKRVCKEVDVJVCRKACSPVCWDTUPVM
      RVEKEVFVLVERKBDSPVEWETUPNUQ $.
      $( [21-Aug-1999] $)
  $}

  $( Parallelogram law for norms.  Remark 3.4(B) of [Beran] p. 98. $)
  normpar $p |- ( ( A e. ~H /\ B e. ~H ) ->
          ( ( ( normh ` ( A -h B ) ) ^ 2 ) + ( ( normh ` ( A +h B ) ) ^ 2 ) ) =
       ( ( 2 x. ( ( normh ` A ) ^ 2 ) ) + ( 2 x. ( ( normh ` B ) ^ 2 ) ) ) ) $=
    ( chil wcel cmv co cno cfv c2 cexp cva caddc cmul c0v opreq1 fveq2d opreq1d
    wceq cif opreq2d opreq12d eqeq12d opreq2 ax-hv0cl elimel normpari dedth2h
    fveq2 ) ACDZBCDZABEFZGHZIJFZABKFZGHZIJFZLFZIAGHZIJFZMFZIBGHZIJFZMFZLFZRUIAN
    SZBEFZGHZIJFZVEBKFZGHZIJFZLFZIVEGHZIJFZMFZVCLFZRVEUJBNSZEFZGHZIJFZVEVQKFZGH
    ZIJFZLFZVOIVQGHZIJFZMFZLFZRABNNAVERZUQVLVDVPWIUMVHUPVKLWIULVGIJWIUKVFGAVEBE
    OPQWIUOVJIJWIUNVIGAVEBKOPQUAWIUTVOVCLWIUSVNIMWIURVMIJAVEGUHQTQUBBVQRZVLWDVP
    WHWJVHVTVKWCLWJVGVSIJWJVFVRGBVQVEEUCPQWJVJWBIJWJVIWAGBVQVEKUCPQUAWJVCWGVOLW
    JVBWFIMWJVAWEIJBVQGUHQTTUBVEVQANCUDUEBNCUDUEUFUG $.
    $( [15-Apr-2007] $)

  ${
    normpar2.1 $e |- A e. ~H $.
    normpar2.2 $e |- B e. ~H $.
    normpar2.3 $e |- C e. ~H $.
    $( Corollary of parallelogram law for norms.  Part of Lemma 3.6 of [Beran]
       p. 100. $)
    normpar2i $p |- ( ( normh ` ( A -h B ) ) ^ 2 ) =
                   ( ( ( 2 x. ( ( normh ` ( A -h C ) ) ^ 2 ) ) +
                       ( 2 x. ( ( normh ` ( B -h C ) ) ^ 2 ) ) )
          - ( ( normh ` ( ( A +h B ) -h ( 2 .h C ) ) ) ^ 2 ) ) $=
      ( c2 cmv co cno cfv cexp cmul caddc cva recni 2cn c4 cdiv opreq1i csm 2re
      cmin hvsubcli normcli remulcli readdcli hvaddcli hvmulcli 4re mulcli 2ne0
      resqcli divdiri addcomi cabs c1 cneg hvsubvali hvcomi hvnegdii hvsubcan2i
      opreq12i ax-1cn negcli hvadd23i 3eqtr4i 3eqtri hvsubdistr1i eqtr4i fveq2i
      eqtri norm-iii-i cc0 cle wbr wceq 0re 2pos ltleii absidi ax-mp sqmuli sq2
      normpari 3eqtr2i divcan3i div23i 4d2e2 3eqtr3i subaddrii eqcomi ) GACHIZJ
      KZGLIZMIZGBCHIZJKZGLIZMIZNIZABOIZGCUAIZHIZJKZGLIZUCIABHIZJKZGLIZXAXFXIXAW
      PWTGWOUBWNWMACDFUDZUEZUMZUFGWSUBWRWQBCEFUDZUEZUMZUFUGPXFXEXDXBXCABDEUHZGC
      QFUIZUDZUEUMPZXIXHXGABDEUDZUEUMPZRWOMIZRWSMIZNIZGSIZYBGSIZYCGSIZNIXFXINIZ
      XAYBYCGRWORUJPZWOXLPZUKZRWSYIWSXOPZUKZQULUNYEGXFMIZGXIMIZNIZGSIYNGSIZYOGS
      IZNIYHYDYPGSYDYCYBNIXDXGHIZJKZGLIZXDXGOIZJKZGLIZNIYPYBYCYKYMUOUUAYCUUDYBN
      UUAGWRMIZGLIGGLIZWSMIYCYTUUEGLYTGWQUAIZJKGUPKZWRMIUUEYSUUGJYSGBUAIZXCHIZU
      UGYSXDUQURZXGUAIZOIXBUUKXCUAIZOIZUULOIZUUJXDXGXRXTUSXDUUNUULOXBXCXPXQUSZT
      XBUULOIZUUMOIUUIUUMOIUUOUUJUUQUUIUUMOUUQBAOIZBAHIZOIUUIXBUURUULUUSOABDEUT
      ABDEVAVCBAEDVBVLTXBUUMUULXPUUKXCUQVDVEZXQUIZUUKXGUUTXTUIVFUUIXCGBQEUIXQUS
      VGVHGBCQEFVIVJVKGWQQXMVMUUHGWRMVNGVOVPUUHGVQVNGVRUBVSVTGUBWAWBZTVHTGWRQWR
      XNPWCUUFRWSMWDTVHUUDGWNMIZGLIUUFWOMIYBUUCUVCGLUUCGWMUAIZJKUUHWNMIUVCUUBUV
      DJUUBGAUAIZXCHIZUVDUUBUUNXGOIXBXGOIZUUMOIZUVFXDUUNXGOUUPTXBUUMXGXPUVAXTVF
      UVHUVEUUMOIUVFUVGUVEUUMOABDEVBTUVEXCGAQDUIXQUSVJVHGACQDFVIVJVKGWMQXJVMUUH
      GWNMUVBTVHTGWNQWNXKPWCUUFRWOMWDTVHVCXDXGXRXTWEWFTYNYOGGXFQXSUKGXIQYAUKQUL
      UNYQXFYRXINXFGXSQULWGXIGYAQULWGVCVHYFWPYGWTNYFRGSIZWOMIWPRWOGYIYJQULWHUVI
      GWOMWITVLYGUVIWSMIWTRWSGYIYLQULWHUVIGWSMWITVLVCWJWKWL $.
      $( [5-Oct-1999] $)
  $}

  ${
    polid2.1 $e |- A e. ~H $.
    polid2.2 $e |- B e. ~H $.
    polid2.3 $e |- C e. ~H $.
    polid2.4 $e |- D e. ~H $.
    $( Generalized polarization identity.  Generalization of Exercise 4(a) of
       [ReedSimon] p. 63. $)
    polid2i $p |- ( A .ih B ) = ( ( ( ( ( A +h C ) .ih ( D +h B ) ) -
                ( ( A -h C ) .ih ( D -h B ) ) ) +
       ( _i x. ( ( ( A +h ( _i .h C ) ) .ih ( D +h ( _i .h B ) ) ) -
               ( ( A -h ( _i .h C ) ) .ih ( D -h ( _i .h B ) ) ) ) ) ) / 4 ) $=
      ( c4 csp co cmul cmin ci caddc hicli c2 addcli wcel ax-icn cdiv cva recni
      cmv csm 4re 4pos gt0ne0ii divcan3i 2cn subcli adddii cc wceq ppncan mp3an
      2timesi eqtr4i opreq2i mulassi opreq1i 3eqtr2ri pnncani normlem8 normlem9
      2t2e4 opreq12i 3eqtr4i cneg hvmulcli ccj cfv chil his5 cji ax-his3 eqtr3i
      3eqtri negcli mulcli mul12i c1 mulneg2i ixi negeqi ax-1cn negnegi mulid2i
      eqtri 3eqtr3i mulm1i negsubi 3eqtr2i ) IABJKZLKZIUAKWNACUBKDBUBKJKZACUDKD
      BUDKJKZMKZNANCUEKZUBKDNBUEKZUBKJKZAWSUDKDWTUDKJKZMKZLKZOKZIUAKWNIABEFPZIU
      FUCIUFUGUHUIWOXEIUAQWNCDJKZOKZWNXGMKZOKZLKZQXHLKZQXILKZOKWOXEQXHXIUJWNXGX
      FCDGHPZRZWNXGXFXNUKULXKQQWNLKZLKQQLKZWNLKWOXJXPQLXJWNWNOKZXPWNUMSZXGUMSXS
      XJXRUNXFXNXFWNXGWNUOUPWNXFUQURUSQQWNUJUJXFUTXQIWNLVFVAVBWRXLXDXMOADJKZCBJ
      KZOKZXHOKZYBXHMKZMKXHXHOKWRXLYBXHXHXTYAADEHPZCBGFPRXOXOVCWPYCWQYDMACDBEGH
      FVDACDBEGHFVEVGXHXOUQVHXDNQNVIZWNLKZNXGLKZOKZLKZLKQNYILKZLKXMXCYJNLXCXTWS
      WTJKZOKZAWTJKZWSDJKZOKZOKZYMYPMKZMKYPYPOKZYJXAYQXBYRMAWSDWTENCTGVJZHNBTFV
      JZVDAWSDWTEYTHUUAVEVGYMYPYPXTYLYEWSWTYTUUAPRYNYOAWTEUUAPWSDYTHPRZUUBVCQYP
      LKYSYJYPUUBUQYPYIQLYNYGYOYHOYNNVKVLZWNLKZYGNUMSZAVMSBVMSYNUUDUNTEFNABVNUP
      UUCYFWNLVOVAWIUUECVMSDVMSYOYHUNTGHNCDVPUPVGUSVQVRUSQNYIUJTYGYHYFWNNTVSZXF
      VTZNXGTXNVTZRWAYKXIQLYKNYGLKZNYHLKZOKWNXGVIZOKXINYGYHTUUGUUHULUUIWNUUJUUK
      ONYFLKZWNLKWBWNLKUUIWNUULWBWNLUULNNLKZVIWBVIZVIWBNNTTWCUUMUUNWDWEWBWFWGVR
      VANYFWNTUUFXFUTWNXFWHWJUUMXGLKUUNXGLKUUJUUKUUMUUNXGLWDVANNXGTTXNUTXGXNWKW
      JVGWNXGXFXNWLVRUSWMVGVHVAVQ $.
      $( [30-Jun-2005] $)
  $}

  ${
    polid.1 $e |- A e. ~H $.
    polid.2 $e |- B e. ~H $.
    $( Polarization identity.  Recovers inner product from norm.  Exercise 4(a)
       of [ReedSimon] p. 63.  The outermost operation is + instead of - due to
       our mathematicians' (rather than physicists') version of axiom
       ~ ax-his3 . $)
    polidi $p |- ( A .ih B ) = ( ( ( ( ( normh ` ( A +h B ) ) ^ 2 ) -
                ( ( normh ` ( A -h B ) ) ^ 2 ) ) +
       ( _i x. ( ( ( normh ` ( A +h ( _i .h B ) ) ) ^ 2 ) -
                ( ( normh ` ( A -h ( _i .h B ) ) ) ^ 2 ) ) ) ) / 4 ) $=
      ( csp co cva cmv cmin ci cmul caddc c4 cdiv cno cfv cexp normsqi opreq12i
      c2 csm polid2i hvaddcli hvsubcli ax-icn hvmulcli opreq2i opreq1i eqtr4i )
      ABEFABGFZUJEFZABHFZULEFZIFZJAJBUAFZGFZUPEFZAUOHFZUREFZIFZKFZLFZMNFUJOPTQF
      ZULOPTQFZIFZJUPOPTQFZUROPTQFZIFZKFZLFZMNFABBACDDCUBVJVBMNVEUNVIVALVCUKVDU
      MIUJABCDUCRULABCDUDRSVHUTJKVFUQVGUSIUPAUOCJBUEDUFZUCRURAUOCVKUDRSUGSUHUI
      $.
      $( [30-Jun-2005] $)
  $}

  $( Polarization identity.  Recovers inner product from norm.  Exercise 4(a)
     of [ReedSimon] p. 63.  The outermost operation is + instead of - due to
     our mathematicians' (rather than physicists') version of axiom
     ~ ax-his3 . $)
  polid $p |- ( ( A e. ~H /\ B e. ~H ) ->
           ( A .ih B ) = ( ( ( ( ( normh ` ( A +h B ) ) ^ 2 ) -
              ( ( normh ` ( A -h B ) ) ^ 2 ) ) +
       ( _i x. ( ( ( normh ` ( A +h ( _i .h B ) ) ) ^ 2 ) -
                ( ( normh ` ( A -h ( _i .h B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
    ( csp co cva cno cfv c2 cexp cmv cmin ci cmul caddc cdiv c0v fveq2d opreq1d
    c4 opreq12d chil wcel csm cif opreq1 opreq2d eqeq12d opreq2 ax-hv0cl elimel
    wceq polidi dedth2h ) AUAUBZBUAUBZABCDZABEDZFGZHIDZABJDZFGZHIDZKDZLALBUCDZE
    DZFGZHIDZAVDJDZFGZHIDZKDZMDZNDZSODZUKUNAPUDZBCDZVOBEDZFGZHIDZVOBJDZFGZHIDZK
    DZLVOVDEDZFGZHIDZVOVDJDZFGZHIDZKDZMDZNDZSODZUKVOUOBPUDZCDZVOWNEDZFGZHIDZVOW
    NJDZFGZHIDZKDZLVOLWNUCDZEDZFGZHIDZVOXCJDZFGZHIDZKDZMDZNDZSODZUKABPPAVOUKZUP
    VPVNWMAVOBCUEXNVMWLSOXNVCWCVLWKNXNUSVSVBWBKXNURVRHIXNUQVQFAVOBEUEQRXNVAWAHI
    XNUTVTFAVOBJUEQRTXNVKWJLMXNVGWFVJWIKXNVFWEHIXNVEWDFAVOVDEUEQRXNVIWHHIXNVHWG
    FAVOVDJUEQRTUFTRUGBWNUKZVPWOWMXMBWNVOCUHXOWLXLSOXOWCXBWKXKNXOVSWRWBXAKXOVRW
    QHIXOVQWPFBWNVOEUHQRXOWAWTHIXOVTWSFBWNVOJUHQRTXOWJXJLMXOWFXFWIXIKXOWEXEHIXO
    WDXDFXOVDXCVOEBWNLUCUHZUFQRXOWHXHHIXOWGXGFXOVDXCVOJXPUFQRTUFTRUGVOWNAPUAUIU
    JBPUAUIUJULUM $.
    $( [17-Nov-2007] $)

