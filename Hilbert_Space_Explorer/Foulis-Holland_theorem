$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Foulis-Holland theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Foulis-Holland Theorem.  If any 2 pairs in a triple of orthomodular
     lattice elements commute, the triple is distributive.  First of two
     parts.  Theorem 5 of [Kalmbach] p. 25. $)
  fh1 $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\ ( A C_H B /\ A C_H C ) )
             -> ( A i^i ( B vH C ) ) = ( ( A i^i B ) vH ( A i^i C ) ) ) $=
    ( cch wcel w3a ccm wbr wa cin chj co csh wss cort cfv c0h wceq chincl
    chjcl syl2an anandis sylan2 chsh syl jca 3impb adantr ledi incom a1i
    chdmj1 chdmm1 ineqan12d eqtrd ineq12d 3impdi inass cmcm2 wb choccl cmbr3
    bitrd biimpa 3adantl3 adantrr 3adantl2 adantrl inindi 3eqtr4g ineq2d
    syl5eq in12 syl6eq chocin eqtr3d chm0 sylan9eqr 3eqtrd pjoml syl12anc
    eqcomd ) ADEZBDEZCDEZFZABGHZACGHZIZIZABJZACJZKLZABCKLZJZWJWMDEZWOMEZIZWMWON
    ZWOWMOPZJZQRWMWORWFWRWIWCWDWEWRWCWDWEIZIZWPWQWCWDWEWPWCWDIZWKDEZWLDEZWPWCWE
    IZABSZACSZWKWLTUAUBXCWODEZWQXBWCWNDEZXJBCTZAWNSUCWOUDUEUFUGUHWFWSWIABCUIUHW
    JXAWNAJZAOPZBOPZKLZXNCOPZKLZJZJZAWNXOXQJZJZJZQWFXAXTRZWIWCWDWEYDXDXGIZWOXMW
    TXSWOXMRYEAWNUJUKYEWTWKOPZWLOPZJZXSXDXEXFWTYHRXGXHXIWKWLULUAXDXGYFXPYGXRABU
    MACUMUNUOUPUQUHWJXTWNAYAJZJZYCWJXTWNAXSJZJYJWNAXSURWJYKYIWNWJAXPJZAXRJZJAXO
    JZAXQJZJYKYIWJYLYNYMYOWFWGYLYNRZWHWCWDWGYPWEXDWGYPXDWGAXOGHZYPABUSWDWCXODEY
    QYPUTBVAAXOVBUCVCVDVEVFWFWHYMYORZWGWCWEWHYRWDXGWHYRXGWHAXQGHZYRACUSWEWCXQDE
    YSYRUTCVAAXQVBUCVCVDVGVHUPAXPXRVIAXOXQVIVJVKVLWNAYAVMVNWFYCQRZWIWCWDWEYTXBW
    CYCAQJQXBYBQAXBWNWNOPZJZYBQXBUUAYAWNBCULVKXBXKUUBQRXLWNVOUEVPVKAVQVRUGUHVSW
    MWOVTWAWB $.
    $( [14-Jun-2006] $)

  $( Foulis-Holland Theorem.  If any 2 pairs in a triple of orthomodular
     lattice elements commute, the triple is distributive.  Second of two
     parts.  Theorem 5 of [Kalmbach] p. 25. $)
  fh2 $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\ ( B C_H A /\ B C_H C ) )
             -> ( A i^i ( B vH C ) ) = ( ( A i^i B ) vH ( A i^i C ) ) ) $=
    ( cch wcel w3a ccm wbr wa cin chj co csh wss cort cfv c0h wceq chincl
    chjcl syl2an anandis sylan2 chsh syl jca 3impb adantr ledi chdmj1 chdmm1
    ineq1d eqtrd 3impdi ineq2d in4 cmcm2 cmcm wb choccl cmbr3 3bitr3d biimpa
    incom syl6eq 3adantl3 adantrr syl5eq ococ opreq1d 3ad2ant2 cmcm3 sylan
    bitrd 3adantl1 adantrl eqtr3d inass in12 eqtr4i chocin 3adant2 chm0 3eqtrd
    pjoml syl12anc eqcomd ) ADEZBDEZCDEZFZBAGHZBCGHZIZIZABJZACJZKLZABCKLZJZWOWR
    DEZWTMEZIZWRWTNZWTWROPZJZQRWRWTRWKXCWNWHWIWJXCWHWIWJIZIZXAXBWHWIWJXAWHWIIZW
    PDEZWQDEZXAWHWJIZABSZACSZWPWQTUAUBXHWTDEZXBXGWHWSDEXOBCTAWSSUCWTUDUEUFUGUHW
    KXDWNABCUIUHWOXFBOPZWSJZAWQOPZJZJZXPCJZXSJZQWOXFXPAJZWSXRJZJZXTWOXFWTAOPXPK
    LZXRJZJZYEWKXFYHRWNWKXEYGWTWHWIWJXEYGRXIXLIZXEWPOPZXRJZYGXIXJXKXEYKRXLXMXNW
    PWQUJUAYIYJYFXRXIYJYFRXLABUKUHULUMUNUOUHWOYHAYFJZYDJYEAWSYFXRUPWOYLYCYDWKWL
    YLYCRZWMWHWIWLYMWJXIWLIYLAXPJZYCXIWLYLYNRZXIABGHAXPGHZWLYOABUQABURWIWHXPDEZ
    YPYOUSBUTZAXPVAUCVBVCAXPVDVEVFVGULVHUMXPAWSXRUPVEWOXQYAXSWOXPXPOPZCKLZJZXQY
    AWKUUAXQRZWNWIWHUUBWJWIYTWSXPWIYSBCKBVIVJUOVKUHWKWMUUAYARZWLWIWJWMUUCWHXGWM
    UUCXGWMXPCGHZUUCBCVLWIYQWJUUDUUCUSYRXPCVAVMVNVCVOVPVQULWKYBQRWNWKYBXPQJZQWH
    WJYBUUERWIXLYBXPCXSJZJUUEXPCXSVRXLUUFQXPXLUUFWQXRJZQUUFACXRJJUUGCAXRVSACXRV
    RVTXLXKUUGQRXNWQWAUEVHUOVHWBWIWHUUEQRZWJWIYQUUHYRXPWCUEVKUMUHWDWRWTWEWFWG
    $.
    $( [14-Jun-2006] $)

  $( A lattice element that commutes with two others also commutes with their
     join.  Theorem 4.2 of [Beran] p. 49. $)
  cm2j $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\ ( A C_H B /\ A C_H C ) )
             -> A C_H ( B vH C ) ) $=
    ( cch wcel w3a ccm wbr wa chj co cin cort cfv wceq cmcm wb cmbr ancoms
    bitrd biimpa incom opreq12i syl6eq 3adantl3 adantrr 3adantl2 adantrl
    opreq12d chincl choccl sylan jca chj4 syl2an 3impdi adantr fh1 syl5reqr id
    3anim123i cmcm3 3adant3 3adant2 anbi12d syl11anc 3eqtrd ex chjcl sylan2
    3impb sylibrd imp ) ADEZBDEZCDEZFZABGHZACGHZIZABCJKZGHZVQVTWAWAALZWAAMNZLZJ
    KZOZWBVQVTWGVQVTIZWAABLZWDBLZJKZACLZWDCLZJKZJKZWIWLJKZWJWMJKZJKZWFWHBWKCWNJ
    VQVRBWKOZVSVNVOVRWSVPVNVOIZVRIBBALZBWDLZJKZWKWTVRBXCOZWTVRBAGHZXDABPVOVNXEX
    DQBARSTUAXAWIXBWJJBAUBBWDUBUCUDUEUFVQVSCWNOZVRVNVPVSXFVOVNVPIZVSICCALZCWDLZ
    JKZWNXGVSCXJOZXGVSCAGHZXKACPVPVNXLXKQCARSTUAXHWLXIWMJCAUBCWDUBUCUDUGUHUIVQW
    OWROZVTVNVOVPXMWTWIDEZWJDEZIWLDEZWMDEZIXMXGWTXNXOABUJVNWDDEZVOXOAUKZWDBUJUL
    UMXGXPXQACUJVNXRVPXQXSWDCUJULUMWIWJWLWMUNUOUPUQWHWPWCWQWEJWHWCAWALWPAWAUBAB
    CURUSWHWEWDWALZWQWDWAUBWHXRVOVPFZWDBGHZWDCGHZIZXTWQOVQYAVTVNXRVOVOVPVPXSVOU
    TVPUTVAUQVQVTYDVQVRYBVSYCVNVOVRYBQVPABVBVCVNVPVSYCQVOACVBVDVEUAWDBCURVFUSUI
    VGVHVNVOVPWBWGQZVOVPIVNWADEZYEBCVIVNYFIWBWAAGHZWGAWAPYFVNYGWGQWAARSTVJVKVLV
    M $.
    $( [15-Jun-2006] $)

  ${
    fh1.1 $e |- A e. CH $.
    fh1.2 $e |- B e. CH $.
    fh1.3 $e |- C e. CH $.
    fh1.4 $e |- A C_H B $.
    fh1.5 $e |- A C_H C $.
    $( Foulis-Holland Theorem.  If any 2 pairs in a triple of orthomodular
       lattice elements commute, the triple is distributive.  First of two
       parts.  Theorem 5 of [Kalmbach] p. 25. $)
    fh1i $p |- ( A i^i ( B vH C ) ) = ( ( A i^i B ) vH ( A i^i C ) ) $=
      ( cch wcel w3a ccm wbr wa chj co cin wceq 3pm3.2i pm3.2i fh1 mp2an ) AIJZ
      BIJZCIJZKABLMZACLMZNABCOPQABQACQOPRUCUDUEDEFSUFUGGHTABCUAUB $.
      $( [7-Aug-2004] $)

    $( Foulis-Holland Theorem.  If any 2 pairs in a triple of orthomodular
       lattice elements commute, the triple is distributive.  Second of two
       parts.  Theorem 5 of [Kalmbach] p. 25. $)
    fh2i $p |- ( B i^i ( A vH C ) ) = ( ( B i^i A ) vH ( B i^i C ) ) $=
      ( cch wcel w3a ccm wbr wa chj co cin wceq 3pm3.2i pm3.2i fh2 mp2an ) BIJZ
      AIJZCIJZKABLMZACLMZNBACOPQBAQBCQOPRUCUDUEEDFSUFUGGHTBACUAUB $.
      $( [7-Aug-2004] $)

    $( Variation of the Foulis-Holland Theorem. $)
    fh3i $p |- ( A vH ( B i^i C ) ) = ( ( A vH B ) i^i ( A vH C ) ) $=
      ( cin chj co wceq cort cfv choccli cmcm3ii cmcm2ii fh1i chdmm1i ineq2i
      chdmj1i opreq12i 3eqtr4ri chjcli chincli 3eqtr4i chcon3i mpbir ) ABCIZJKZ
      ABJKZACJKZIZLUMMNZUJMNZLUKMNZULMNZJKZAMNZUIMNZIZUNUOUSBMNZCMNZJKZIUSVBIZU
      SVCIZJKVAURUSVBVCADOZBEOCFOUSBVGEABDEGPQUSCVGFACDFHPQRUTVDUSBCEFSTUPVEUQV
      FJABDEUAACDFUAUBUCUKULABDEUDZACDFUDZSAUIDBCEFUEZUAUFUJUMAUIDVJUDUKULVHVIU
      EUGUH $.
      $( [16-Jan-2005] $)

    $( Variation of the Foulis-Holland Theorem. $)
    fh4i $p |- ( B vH ( A i^i C ) ) = ( ( B vH A ) i^i ( B vH C ) ) $=
      ( cin chj co wceq cort cfv choccli cmcm3ii cmcm2ii fh2i chdmm1i ineq2i
      chdmj1i opreq12i 3eqtr4ri chjcli chincli 3eqtr4i chcon3i mpbir ) BACIZJKZ
      BAJKZBCJKZIZLUMMNZUJMNZLUKMNZULMNZJKZBMNZUIMNZIZUNUOUSAMNZCMNZJKZIUSVBIZU
      SVCIZJKVAURVBUSVCADOZBEOCFOVBBVGEABDEGPQVBCVGFACDFHPQRUTVDUSACDFSTUPVEUQV
      FJBAEDUABCEFUAUBUCUKULBAEDUDZBCEFUDZSBUIEACDFUEZUAUFUJUMBUIEVJUDUKULVHVIU
      EUGUH $.
      $( [16-Jan-2005] $)

    $( A lattice element that commutes with two others also commutes with their
       join.  Theorem 4.2 of [Beran] p. 49. $)
    cm2ji $p |- A C_H ( B vH C ) $=
      ( cch wcel w3a ccm wbr wa chj co 3pm3.2i pm3.2i cm2j mp2an ) AIJZBIJZCIJZ
      KABLMZACLMZNABCOPLMUAUBUCDEFQUDUEGHRABCST $.
      $( [11-May-2009] $)

    $( A lattice element that commutes with two others also commutes with their
       meet.  Theorem 4.2 of [Beran] p. 49. $)
    cm2mi $p |- A C_H ( B i^i C ) $=
      ( cin ccm wbr cort cfv chj co choccli cmcm2ii cm2ji chdmm1i breqtrri
      chincli cmcm2i mpbir ) ABCIZJKAUDLMZJKABLMZCLMZNOUEJAUFUGDBEPCFPABDEGQACD
      FHQRBCEFSTAUDDBCEFUAUBUC $.
      $( [11-May-2009] $)
  $}

