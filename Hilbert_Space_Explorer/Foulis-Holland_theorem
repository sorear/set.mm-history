$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Foulis-Holland theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Foulis-Holland Theorem.  If any 2 pairs in a triple of orthomodular
     lattice elements commute, the triple is distributive.  First of two
     parts.  Theorem 5 of [Kalmbach] p. 25. $)
  fh1t $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\ ( A C_H B /\ A C_H C ) )
             -> ( A i^i ( B vH C ) ) = ( ( A i^i B ) vH ( A i^i C ) ) ) $=
    ( cch wcel w3a ccm wbr wa cin chj co csh wss cort cfv c0h wceq pjomlt
    chjclt chinclt syl2an anandis sylan2 chsh syl jca 3impb adantr ledit incom
    a1i chdmj1t chdmm1t ineqan12d eqtrd ineq12d 3impdi cmcm2t wb cmbr3t
    chocclt bitrd biimpa 3adantl3 adantrr 3adantl2 adantrl inindi 3eqtr4g
    ineq2d inass syl5eq in12 syl6eq chocint eqtr3d chm0t sylan9eqr 3eqtrd
    sylanc eqcomd ) ADEZBDEZCDEZFZABGHZACGHZIZIZABJZACJZKLZABCKLZJZWMDEZWOMEZIZ
    WMWONZWOWMOPZJZQRZIWMWORWJWMWOSWFWRWIWCWDWEWRWCWDWEIZIZWPWQWCWDWEWPWKDEZWLD
    EZWPWCWDIZWCWEIZWKWLTABUAZACUAZUBUCXDWODEZWQWCWNDEZXKXCAWNUABCTZUDWOUEUFUGU
    HUIWJWSXBWFWSWIABCUJUIWJXAWNAJZAOPZBOPZKLZXOCOPZKLZJZJZAWNXPXRJZJZJZQWFXAYA
    RZWIWCWDWEYEXGXHIZWOXNWTXTWOXNRYFAWNUKULYFWTWKOPZWLOPZJZXTXEXFWTYIRXGXHWKWL
    UMXIXJUBXGXHYGXQYHXSABUNACUNUOUPUQURUIWJYAWNAYBJZJZYDWJWNAXTJZJYKYAWJYLYJWN
    WJAXQJZAXSJZJAXPJZAXRJZJYLYJWJYMYOYNYPWFWGYMYORZWHWCWDWGYQWEXGWGYQXGWGAXPGH
    ZYQABUSWCXPDEYRYQUTWDAXPVABVBUDVCVDVEVFWFWHYNYPRZWGWCWEWHYSWDXHWHYSXHWHAXRG
    HZYSACUSWCXRDEYTYSUTWEAXRVACVBUDVCVDVGVHUQAXQXSVIAXPXRVIVJVKWNAXTVLVMWNAYBV
    NVOWFYDQRZWIWCWDWEUUAXCWCYDAQJQXCYCQAXCWNWNOPZJZYCQXCUUBYBWNBCUMVKXCXLUUCQR
    XMWNVPUFVQVKAVRVSUHUIVTUGWAWB $.
    $( [14-Jun-2006] $) $( [14-Jun-2006] $)

  $( Foulis-Holland Theorem.  If any 2 pairs in a triple of orthomodular
     lattice elements commute, the triple is distributive.  Second of two
     parts.  Theorem 5 of [Kalmbach] p. 25. $)
  fh2t $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\ ( B C_H A /\ B C_H C ) )
             -> ( A i^i ( B vH C ) ) = ( ( A i^i B ) vH ( A i^i C ) ) ) $=
    ( cch wcel w3a ccm wbr wa cin chj co csh wss cort cfv c0h wceq pjomlt
    chjclt chinclt syl2an anandis sylan2 chsh syl jca 3impb adantr ledit
    chdmj1t chdmm1t ineq1d eqtrd 3impdi ineq2d cmcm2t cmcmt wb cmbr3t chocclt
    3bitr3d biimpa incom syl6eq 3adantl3 adantrr in4 syl5eq ococt opreq1d
    3ad2ant2 cmcm3t sylan bitrd 3adantl1 adantrl eqtr3d chocint in12 inass
    eqtr4 3adant2 chm0t 3eqtrd sylanc eqcomd ) ADEZBDEZCDEZFZBAGHZBCGHZIZIZABJZ
    ACJZKLZABCKLZJZWRDEZWTMEZIZWRWTNZWTWROPZJZQRZIWRWTRWOWRWTSWKXCWNWHWIWJXCWHW
    IWJIZIZXAXBWHWIWJXAWPDEZWQDEZXAWHWIIZWHWJIZWPWQTABUAZACUAZUBUCXIWTDEZXBWHWS
    DEXPXHAWSUABCTUDWTUEUFUGUHUIWOXDXGWKXDWNABCUJUIWOXFBOPZWSJZAWQOPZJZJZXQCJZX
    TJZQWOXFXQAJZWSXSJZJZYAWOXFWTAOPXQKLZXSJZJZYFWKXFYIRWNWKXEYHWTWHWIWJXEYHRXL
    XMIZXEWPOPZXSJZYHXJXKXEYLRXLXMWPWQUKXNXOUBYJYKYGXSXLYKYGRXMABULUIUMUNUOUPUI
    WOAYGJZYEJYFYIWOYMYDYEWKWLYMYDRZWMWHWIWLYNWJXLWLIYMAXQJZYDXLWLYMYORZXLABGHA
    XQGHZWLYPABUQABURWHXQDEZYQYPUSWIAXQUTBVAZUDVBVCAXQVDVEVFVGUMAWSYGXSVHVIUNXQ
    AWSXSVHVEWOXRYBXTWOXQXQOPZCKLZJZXRYBWKUUBXRRZWNWIWHUUCWJWIUUAWSXQWIYTBCKBVJ
    VKUPVLUIWKWMUUBYBRZWLWIWJWMUUDWHXHWMUUDXHWMXQCGHZUUDBCVMYRWJUUEUUDUSWIXQCUT
    YSVNVOVCVPVQVRUMWKYCQRWNWKYCXQQJZQWHWJYCUUFRWIXMXQCXTJZJUUFYCXMUUGQXQXMWQXS
    JZQUUGXMXKUUHQRXOWQVSUFUUGACXSJJUUHCAXSVTACXSWAWBVIUPXQCXTWAVIWCWIWHUUFQRZW
    JWIYRUUIYSXQWDUFVLUNUIWEUGWFWG $.
    $( [15-Jun-2006] $) $( [14-Jun-2006] $)

  $( A lattice element that commutes with two others also commutes with their
     join.  Theorem 4.2 of [Beran] p. 49. $)
  cm2jt $p |- ( ( ( A e. CH /\ B e. CH /\ C e. CH ) /\ ( A C_H B /\ A C_H C ) )
             -> A C_H ( B vH C ) ) $=
    ( cch wcel w3a ccm wbr wa chj co cin cort cfv wceq cmcmt wb cmbrt ancoms
    bitrd biimpa incom opreq12i syl6eq 3adantl3 adantrr 3adantl2 adantrl
    opreq12d chj4t chinclt chocclt sylan jca syl2an 3impdi adantr fh1t
    syl5reqr id 3anim123i cmcm3t 3adant3 3adant2 anbi12d sylanc 3eqtrd ex
    chjclt sylan2 3impb sylibrd imp ) ADEZBDEZCDEZFZABGHZACGHZIZABCJKZGHZVQVTWA
    WAALZWAAMNZLZJKZOZWBVQVTWGVQVTIZWAABLZWDBLZJKZACLZWDCLZJKZJKZWIWLJKZWJWMJKZ
    JKZWFWHBWKCWNJVQVRBWKOZVSVNVOVRWSVPVNVOIZVRIBBALZBWDLZJKZWKWTVRBXCOZWTVRBAG
    HZXDABPVOVNXEXDQBARSTUAXAWIXBWJJBAUBBWDUBUCUDUEUFVQVSCWNOZVRVNVPVSXFVOVNVPI
    ZVSICCALZCWDLZJKZWNXGVSCXJOZXGVSCAGHZXKACPVPVNXLXKQCARSTUAXHWLXIWMJCAUBCWDU
    BUCUDUGUHUIVQWOWROZVTVNVOVPXMWIDEZWJDEZIWLDEZWMDEZIXMWTXGWIWJWLWMUJWTXNXOAB
    UKWDDEZVOXOVNWDBUKAULZUMUNXGXPXQACUKXRVPXQVNWDCUKXSUMUNUOUPUQWHWPWCWQWEJWHA
    WALWPWCABCURAWAUBUSWHWDWALZWQWEXRVOVPFZWDBGHZWDCGHZIZXTWQOWHWDBCURVQYAVTVNX
    RVOVOVPVPXSVOUTVPUTVAUQVQVTYDVQVRYBVSYCVNVOVRYBQVPABVBVCVNVPVSYCQVOACVBVDVE
    UAVFWDWAUBUSUIVGVHVNVOVPWBWGQZVNWADEZYEVOVPIVNYFIWBWAAGHZWGAWAPYFVNYGWGQWAA
    RSTBCVIVJVKVLVM $.
    $( [11-Oct-2006] $) $( [15-Jun-2006] $)

  ${
    fh1.1 $e |- A e. CH $.
    fh1.2 $e |- B e. CH $.
    fh1.3 $e |- C e. CH $.
    fh1.4 $e |- A C_H B $.
    fh1.5 $e |- A C_H C $.
    $( Foulis-Holland Theorem.  If any 2 pairs in a triple of orthomodular
       lattice elements commute, the triple is distributive.  First of two
       parts.  Theorem 5 of [Kalmbach] p. 25. $)
    fh1 $p |- ( A i^i ( B vH C ) ) = ( ( A i^i B ) vH ( A i^i C ) ) $=
      ( cch wcel w3a ccm wbr wa chj co cin wceq 3pm3.2i pm3.2i fh1t mp2an ) AIJ
      ZBIJZCIJZKABLMZACLMZNABCOPQABQACQOPRUCUDUEDEFSUFUGGHTABCUAUB $.
      $( [13-Aug-2004] $) $( [7-Aug-2004] $)

    $( Foulis-Holland Theorem.  If any 2 pairs in a triple of orthomodular
       lattice elements commute, the triple is distributive.  Second of two
       parts.  Theorem 5 of [Kalmbach] p. 25. $)
    fh2 $p |- ( B i^i ( A vH C ) ) = ( ( B i^i A ) vH ( B i^i C ) ) $=
      ( cch wcel w3a ccm wbr wa chj co cin wceq 3pm3.2i pm3.2i fh2t mp2an ) BIJ
      ZAIJZCIJZKABLMZACLMZNBACOPQBAQBCQOPRUCUDUEEDFSUFUGGHTBACUAUB $.
      $( [16-Sep-2004] $) $( [7-Aug-2004] $)

    $( Variation of the Foulis-Holland Theorem. $)
    fh3 $p |- ( A vH ( B i^i C ) ) = ( ( A vH B ) i^i ( A vH C ) ) $=
      ( cin chj co wceq cort cfv choccl cmcm3i cmcm2i fh1 chdmm1 ineq2i chdmj1
      opreq12i 3eqtr4r chjcl chincl 3eqtr4 chcon3 mpbir ) ABCIZJKZABJKZACJKZIZL
      UMMNZUJMNZLUKMNZULMNZJKZAMNZUIMNZIZUNUOUSBMNZCMNZJKZIUSVBIZUSVCIZJKVAURUS
      VBVCADOZBEOCFOUSBVGEABDEGPQUSCVGFACDFHPQRUTVDUSBCEFSTUPVEUQVFJABDEUAACDFU
      AUBUCUKULABDEUDZACDFUDZSAUIDBCEFUEZUAUFUJUMAUIDVJUDUKULVHVIUEUGUH $.
      $( [22-Jan-2005] $) $( [16-Jan-2005] $)

    $( Variation of the Foulis-Holland Theorem. $)
    fh4 $p |- ( B vH ( A i^i C ) ) = ( ( B vH A ) i^i ( B vH C ) ) $=
      ( cin chj co wceq cort cfv choccl cmcm3i cmcm2i fh2 chdmm1 ineq2i chdmj1
      opreq12i 3eqtr4r chjcl chincl 3eqtr4 chcon3 mpbir ) BACIZJKZBAJKZBCJKZIZL
      UMMNZUJMNZLUKMNZULMNZJKZBMNZUIMNZIZUNUOUSAMNZCMNZJKZIUSVBIZUSVCIZJKVAURVB
      USVCADOZBEOCFOVBBVGEABDEGPQVBCVGFACDFHPQRUTVDUSACDFSTUPVEUQVFJBAEDUABCEFU
      AUBUCUKULBAEDUDZBCEFUDZSBUIEACDFUEZUAUFUJUMBUIEVJUDUKULVHVIUEUGUH $.
      $( [22-Jan-2005] $) $( [16-Jan-2005] $)
  $}

