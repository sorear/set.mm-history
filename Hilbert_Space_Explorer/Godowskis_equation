$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Godowski's equation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    golem1.1 $e |- A e. CH $.
    golem1.2 $e |- B e. CH $.
    golem1.3 $e |- C e. CH $.
    golem1.4 $e |- F = ( ( _|_ ` A ) vH ( A i^i B ) ) $.
    golem1.5 $e |- G = ( ( _|_ ` B ) vH ( B i^i C ) ) $.
    golem1.6 $e |- H = ( ( _|_ ` C ) vH ( C i^i A ) ) $.
    golem1.7 $e |- D = ( ( _|_ ` B ) vH ( B i^i A ) ) $.
    golem1.8 $e |- R = ( ( _|_ ` C ) vH ( C i^i B ) ) $.
    golem1.9 $e |- S = ( ( _|_ ` A ) vH ( A i^i C ) ) $.
    $( Lemma for Godowski's equation. $)
    golem1 $p |- ( f e. States -> ( ( ( f ` F ) + ( f ` G ) ) + ( f ` H ) ) =
                 ( ( ( f ` D ) + ( f ` R ) ) + ( f ` S ) ) ) $=
      ( cv cst wcel cort cfv cin chj co caddc cc wceq axaddass cch cr choccl
      stclt mpi recnd syl3anc axaddcom axaddcl sylanc eqtrd opreq1d add4t
      chincl syl2anc 3eqtr4d stji1 opreq12d incom fveq2i opreq2i syl6eq
      opreq12i 3eqtr4g ) GTZUAUBZAUCUDZABUEZUFUGZVPUDZBUCUDZBCUEZUFUGZVPUDZUHUG
      ZCUCUDZCAUEZUFUGZVPUDZUHUGZWBBAUEZUFUGZVPUDZWGCBUEZUFUGZVPUDZUHUGZVRACUEZ
      UFUGZVPUDZUHUGZHVPUDZIVPUDZUHUGZJVPUDZUHUGDVPUDZEVPUDZUHUGZFVPUDZUHUGVQVR
      VPUDZVSVPUDZUHUGZWBVPUDZWCVPUDZUHUGZUHUGZWGVPUDZWHVPUDZUHUGZUHUGZXNXLUHUG
      ZXRXOUHUGZUHUGZXKXSUHUGZUHUGZWKXBVQXKXNUHUGZXLXOUHUGZUHUGZXTUHUGZXNXRUHUG
      ZYHUHUGZYEUHUGZYAYFVQYGXRUHUGZYHXSUHUGZUHUGZYKXKUHUGZYOUHUGZYJYMVQYNYQYOU
      HVQYNXKYKUHUGZYQXKUIUBZXNUIUBZXRUIUBZYNYSUJVQXKXNXRUKVQXKVQVRULUBXKUMUBAK
      UNVRVPUOUPUQZVQXNVQWBULUBXNUMUBBLUNWBVPUOUPUQZVQXRVQWGULUBXRUMUBCMUNWGVPU
      OUPUQZURYTYKUIUBZYSYQUJVQXKYKUSUUCUUAUUBUUFVQXNXRUTUUDUUEVAZVAVBVCYGUIUBZ
      YHUIUBZUUBXSUIUBZYJYPUJVQYGYHXRXSVDYTUUAUUHVQXKXNUTUUCUUDVAXLUIUBZXOUIUBZ
      UUIVQXLXOUTVQXLVQVSULUBXLUMUBABKLVEVSVPUOUPUQZVQXOVQWCULUBXOUMUBBCLMVEWCV
      PUOUPUQZVAZUUEVQXSVQWHULUBXSUMUBCAMKVEWHVPUOUPUQZVFUUFUUIYTUUJYMYRUJVQYKY
      HXKXSVDUUGUUOUUCUUPVFVGVQXQYIXTUHYTUUKUUAUULXQYIUJVQXKXLXNXOVDUUCUUMUUDUU
      NVFVCVQYDYLYEUHUUAUUKUUBUULYDYLUJVQXNXLXRXOVDUUDUUMUUEUUNVFVCVGVQWFXQWJXT
      UHVQWAXMWEXPUHABVPKLVHBCVPLMVHVICAVPMKVHVIVQWRYDXAYEUHVQWNYBWQYCUHVQWNXNW
      LVPUDZUHUGYBBAVPLKVHUUQXLXNUHWLVSVPBAVJVKVLVMVQWQXRWOVPUDZUHUGYCCBVPMLVHU
      URXOXRUHWOWCVPCBVJVKVLVMVIVQXAXKWSVPUDZUHUGYEACVPKMVHUUSXSXKUHWSWHVPACVJV
      KVLVMVIVGXEWFXFWJUHXCWAXDWEUHHVTVPNVKIWDVPOVKVNJWIVPPVKVNXIWRXJXAUHXGWNXH
      WQUHDWMVPQVKEWPVPRVKVNFWTVPSVKVNVO $.
      $( [10-Nov-2002] $)


    $( Lemma for Godowski's equation. $)
    golem2 $p |- ( f e. States ->
              ( ( f ` ( ( F i^i G ) i^i H ) ) = 1 -> ( f ` D ) = 1 ) ) $=
      ( cv cst wcel cin cfv c1 wceq caddc co c3 cort chj cch choccl chincl
      chjcl eqeltr stm1add3 golem1 eqeq1d sylibd stadd3 syld ) GTZUAUBZHIUCJUCV
      CUDUEUFZDVCUDZEVCUDUGUHFVCUDUGUHZUIUFZVFUEUFVDVEHVCUDIVCUDUGUHJVCUDUGUHZU
      IUFVHHIJVCHAUJUDZABUCZUKUHULNVJVKAKUMZABKLUNUOUPIBUJUDZBCUCZUKUHULOVMVNBL
      UMZBCLMUNUOUPJCUJUDZCAUCZUKUHULPVPVQCMUMZCAMKUNUOUPUQVDVIVGUIABCDEFGHIJKL
      MNOPQRSURUSUTDEFVCDVMBAUCZUKUHULQVMVSVOBALKUNUOUPEVPCBUCZUKUHULRVPVTVRCBM
      LUNUOUPFVJACUCZUKUHULSVJWAVLACKMUNUOUPVAVB $.
      $( [13-Nov-1999] $)
  $}

  ${
    $d f F $.  $d f G $.  $d f H $.  $d f D $.
    goeq.1 $e |- A e. CH $.
    goeq.2 $e |- B e. CH $.
    goeq.3 $e |- C e. CH $.
    goeq.4 $e |- F = ( ( _|_ ` A ) vH ( A i^i B ) ) $.
    goeq.5 $e |- G = ( ( _|_ ` B ) vH ( B i^i C ) ) $.
    goeq.6 $e |- H = ( ( _|_ ` C ) vH ( C i^i A ) ) $.
    goeq.7 $e |- D = ( ( _|_ ` B ) vH ( B i^i A ) ) $.
    $( Godowski's equation, shown here as a variant equivalent to
       Equation SF of [Godowski] p. 730. $)
    goeq $p |- ( ( F i^i G ) i^i H ) (_ D $=
      ( vf cin cv cfv c1 wceq wi wss cst cort chj co cch choccl chincl chjcl
      eqeltr str eqid golem2 mprg ) EFPZGPZOQZRSTDURRSTUAUQDUBOUCUQDOUPGEFEAUDR
      ZABPZUEUFUGKUSUTAHUHABHIUIUJUKFBUDRZBCPZUEUFUGLVAVBBIUHZBCIJUIUJUKUIGCUDR
      ZCAPZUEUFUGMVDVECJUHCAJHUIUJUKUIDVABAPZUEUFUGNVAVFVCBAIHUIUJUKULABCDVDCBP
      UEUFZUSACPUEUFZOEFGHIJKLMNVGUMVHUMUNUO $.
      $( [10-Nov-2002] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y S $.
    stcltr1.1 $e |- ( ph <-> ( S e. States /\ A. x e. CH A. y e. CH
                ( ( ( S ` x ) = 1 -> ( S ` y ) = 1 ) -> x (_ y ) ) ) $.
    stcltr1.2 $e |- A e. CH $.
    ${
      stcltr1.3 $e |- B e. CH $.
      $( Property of a strong classical state. $)
      stcltr1 $p |- ( ph ->
               ( ( ( S ` A ) = 1 -> ( S ` B ) = 1 ) -> A (_ B ) ) $=
        ( cst wcel cv cfv c1 wceq wi wss cch wral wa fveq2 eqeq1d imbi1d sseq1
        imbi12d imbi2d sseq2 rcla42v mp2an adantl sylbi ) AFJKZBLZFMZNOZCLZFMZN
        OZPZUMUPQZPZCRSBRSZTDFMZNOZEFMZNOZPZDEQZPZGVBVIULDRKERKVBVIPHIVAVIVDURP
        ZDUPQZPBCDERRUMDOZUSVJUTVKVLUOVDURVLUNVCNUMDFUAUBUCUMDUPUDUEUPEOZVJVGVK
        VHVMURVFVDVMUQVENUPEFUAUBUFUPEDUGUEUHUIUJUK $.
        $( [24-Oct-1999] $)
    $}

    $( Property of a strong classical state. $)
    stcltr2 $p |- ( ph -> ( ( S ` A ) = 1 -> A = H~ ) ) $=
      ( cfv c1 wceq chil wss wi helch stcltr1 ax-1 syl5 eqss chssi mpbiran
      syl6ibr ) ADEHIJZKDLZDKJZAKEHIJZUBMUCUBABCKDEFNGOUBUEPQUDDKLUCDKRDGSTUA
      $.
      $( [24-Oct-1999] $)

    ${
      stcltrlem1.3 $e |- B e. CH $.
      $( Lemma for strong classical state theorem. $)
      stcltrlem1 $p |- ( ph -> ( ( S ` B ) = 1 ->
                    ( S ` ( ( _|_ ` A ) vH ( A i^i B ) ) ) = 1 ) ) $=
        ( cfv c1 wceq cort cin chj co wa caddc cst wcel cv wi wss cch wral
        pm3.26bi stji1 syl adantr chil stcltr2 imp ineq2 chm1 syl6eq fveq2d
        opreq2d cc axaddcom cr choccl stclt mpi recnd sylanc sto1 eqtrd 3eqtrd
        ex ) AEFJKLZDMJZDENZOPFJZKLAVJQZVMVKFJZVLFJZRPZVODFJZRPZKAVMVQLZVJAFSTZ
        VTAWABUAZFJKLCUAZFJKLUBWBWCUCUBCUDUEBUDUEGUFZDEFHIUGUHUIVNVPVRVORVNVLDF
        VNEUJLZVLDLAVJWEABCEFGIUKULWEVLDUJNDEUJDUMDHUNUOUHUPUQVNWAVSKLAWAVJWDUI
        WAVSVRVORPZKVOURTVRURTVSWFLWAVOVRUSWAVOWAVKUDTVOUTTDHVAVKFVBVCVDWAVRWAD
        UDTVRUTTHDFVBVCVDVEDFHVFVGUHVHVI $.
        $( [24-Oct-1999] $)

      $( Lemma for strong classical state theorem. $)
      stcltrlem2 $p |- ( ph -> B (_ ( ( _|_ ` A ) vH ( A i^i B ) ) ) $=
        ( cfv c1 wceq cort cin chj co wi wss stcltrlem1 choccl chincl chjcl
        stcltr1 mpd ) AEFJKLDMJZDENZOPZFJKLQEUGRABCDEFGHISABCEUGFGIUEUFDHTDEHIU
        AUBUCUD $.
        $( [24-Oct-1999] $)
    $}
  $}

  ${
    $d x y s A $.  $d x y s B $.
    stcltrth.1 $e |- A e. CH $.
    stcltrth.2 $e |- B e. CH $.
    stcltrth.3 $e |- E. s e. States A. x e. CH A. y e. CH
                ( ( ( s ` x ) = 1 -> ( s ` y ) = 1 ) -> x (_ y ) $.
    $( Strong classical state theorem.  If there exists a "strong classical"
       state on lattice ` CH ` , then any two elements of the lattice commute,
       i.e., the lattice is distributive.  (Proof due to Mladen Pavicic.)
       Theorem 3.3 of [MegillPavicic] p. 2344. $)
    stcltrth $p |- B (_ ( ( _|_ ` A ) vH ( A i^i B ) ) $=
      ( cv cfv c1 wceq wi wss cch wral cst wrex cort cin chj co wcel wa pm4.2
      stcltrlem2 r19.23aiva ax-mp ) AIZEIZJKLBIZUJJKLMUIUKNMBOPAOPZEQRDCSJCDTUA
      UBNZHULUMEQUJQUCULUDZABCDUJUNUEFGUFUGUH $.
      $( [24-Oct-1999] $)
  $}

