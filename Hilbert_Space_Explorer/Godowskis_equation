$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Godowski's equation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    golem1.1 $e |- A e. CH $.
    golem1.2 $e |- B e. CH $.
    golem1.3 $e |- C e. CH $.
    golem1.4 $e |- F = ( ( _|_ ` A ) vH ( A i^i B ) ) $.
    golem1.5 $e |- G = ( ( _|_ ` B ) vH ( B i^i C ) ) $.
    golem1.6 $e |- H = ( ( _|_ ` C ) vH ( C i^i A ) ) $.
    golem1.7 $e |- D = ( ( _|_ ` B ) vH ( B i^i A ) ) $.
    golem1.8 $e |- R = ( ( _|_ ` C ) vH ( C i^i B ) ) $.
    golem1.9 $e |- S = ( ( _|_ ` A ) vH ( A i^i C ) ) $.
    $( Lemma for Godowski's equation. $)
    golem1 $p |- ( f e. States -> ( ( ( f ` F ) + ( f ` G ) ) + ( f ` H ) ) =
                 ( ( ( f ` D ) + ( f ` R ) ) + ( f ` S ) ) ) $=
      ( cv cst wcel cort cfv cin chj co caddc cc wceq cch cr choccli stcl mpi
      recnd addass syl111anc addcl syl11anc addcom eqtrd opreq1d chincli add4
      syl22anc 3eqtr4d stji1i opreq12d incom fveq2i opreq2i syl6eq opreq12i
      3eqtr4g ) GTZUAUBZAUCUDZABUEZUFUGZVPUDZBUCUDZBCUEZUFUGZVPUDZUHUGZCUCUDZCA
      UEZUFUGZVPUDZUHUGZWBBAUEZUFUGZVPUDZWGCBUEZUFUGZVPUDZUHUGZVRACUEZUFUGZVPUD
      ZUHUGZHVPUDZIVPUDZUHUGZJVPUDZUHUGDVPUDZEVPUDZUHUGZFVPUDZUHUGVQVRVPUDZVSVP
      UDZUHUGZWBVPUDZWCVPUDZUHUGZUHUGZWGVPUDZWHVPUDZUHUGZUHUGZXNXLUHUGZXRXOUHUG
      ZUHUGZXKXSUHUGZUHUGZWKXBVQXKXNUHUGZXLXOUHUGZUHUGZXTUHUGZXNXRUHUGZYHUHUGZY
      EUHUGZYAYFVQYGXRUHUGZYHXSUHUGZUHUGZYKXKUHUGZYOUHUGZYJYMVQYNYQYOUHVQYNXKYK
      UHUGZYQVQXKUIUBZXNUIUBZXRUIUBZYNYSUJVQXKVQVRUKUBXKULUBAKUMVRVPUNUOUPZVQXN
      VQWBUKUBXNULUBBLUMWBVPUNUOUPZVQXRVQWGUKUBXRULUBCMUMWGVPUNUOUPZXKXNXRUQURV
      QYTYKUIUBZYSYQUJUUCVQUUAUUBUUFUUDUUEXNXRUSUTZXKYKVAUTVBVCVQYGUIUBZYHUIUBZ
      UUBXSUIUBZYJYPUJVQYTUUAUUHUUCUUDXKXNUSUTVQXLUIUBZXOUIUBZUUIVQXLVQVSUKUBXL
      ULUBABKLVDVSVPUNUOUPZVQXOVQWCUKUBXOULUBBCLMVDWCVPUNUOUPZXLXOUSUTZUUEVQXSV
      QWHUKUBXSULUBCAMKVDWHVPUNUOUPZYGYHXRXSVEVFVQUUFUUIYTUUJYMYRUJUUGUUOUUCUUP
      YKYHXKXSVEVFVGVQXQYIXTUHVQYTUUKUUAUULXQYIUJUUCUUMUUDUUNXKXLXNXOVEVFVCVQYD
      YLYEUHVQUUAUUKUUBUULYDYLUJUUDUUMUUEUUNXNXLXRXOVEVFVCVGVQWFXQWJXTUHVQWAXMW
      EXPUHABVPKLVHBCVPLMVHVICAVPMKVHVIVQWRYDXAYEUHVQWNYBWQYCUHVQWNXNWLVPUDZUHU
      GYBBAVPLKVHUUQXLXNUHWLVSVPBAVJVKVLVMVQWQXRWOVPUDZUHUGYCCBVPMLVHUURXOXRUHW
      OWCVPCBVJVKVLVMVIVQXAXKWSVPUDZUHUGYEACVPKMVHUUSXSXKUHWSWHVPACVJVKVLVMVIVG
      XEWFXFWJUHXCWAXDWEUHHVTVPNVKIWDVPOVKVNJWIVPPVKVNXIWRXJXAUHXGWNXHWQUHDWMVP
      QVKEWPVPRVKVNFWTVPSVKVNVO $.
      $( [10-Nov-2002] $)


    $( Lemma for Godowski's equation. $)
    golem2 $p |- ( f e. States ->
              ( ( f ` ( ( F i^i G ) i^i H ) ) = 1 -> ( f ` D ) = 1 ) ) $=
      ( cv cst wcel cin cfv c1 wceq caddc co c3 cort chj cch choccli chincli
      chjcli eqeltri stm1add3i golem1 eqeq1d sylibd stadd3i syld ) GTZUAUBZHIUC
      JUCVCUDUEUFZDVCUDZEVCUDUGUHFVCUDUGUHZUIUFZVFUEUFVDVEHVCUDIVCUDUGUHJVCUDUG
      UHZUIUFVHHIJVCHAUJUDZABUCZUKUHULNVJVKAKUMZABKLUNUOUPIBUJUDZBCUCZUKUHULOVM
      VNBLUMZBCLMUNUOUPJCUJUDZCAUCZUKUHULPVPVQCMUMZCAMKUNUOUPUQVDVIVGUIABCDEFGH
      IJKLMNOPQRSURUSUTDEFVCDVMBAUCZUKUHULQVMVSVOBALKUNUOUPEVPCBUCZUKUHULRVPVTV
      RCBMLUNUOUPFVJACUCZUKUHULSVJWAVLACKMUNUOUPVAVB $.
      $( [13-Nov-1999] $)
  $}

  ${
    $d f F $.  $d f G $.  $d f H $.  $d f D $.
    goeq.1 $e |- A e. CH $.
    goeq.2 $e |- B e. CH $.
    goeq.3 $e |- C e. CH $.
    goeq.4 $e |- F = ( ( _|_ ` A ) vH ( A i^i B ) ) $.
    goeq.5 $e |- G = ( ( _|_ ` B ) vH ( B i^i C ) ) $.
    goeq.6 $e |- H = ( ( _|_ ` C ) vH ( C i^i A ) ) $.
    goeq.7 $e |- D = ( ( _|_ ` B ) vH ( B i^i A ) ) $.
    $( Godowski's equation, shown here as a variant equivalent to Equation SF
       of [Godowski] p. 730. $)
    goeqi $p |- ( ( F i^i G ) i^i H ) C_ D $=
      ( vf cin cv cfv c1 wceq wi wss cst cort chj co cch choccli chincli
      chjcli eqeltri stri eqid golem2 mprg ) EFPZGPZOQZRSTDURRSTUAUQDUBOUCUQDOU
      PGEFEAUDRZABPZUEUFUGKUSUTAHUHABHIUIUJUKFBUDRZBCPZUEUFUGLVAVBBIUHZBCIJUIUJ
      UKUIGCUDRZCAPZUEUFUGMVDVECJUHCAJHUIUJUKUIDVABAPZUEUFUGNVAVFVCBAIHUIUJUKUL
      ABCDVDCBPUEUFZUSACPUEUFZOEFGHIJKLMNVGUMVHUMUNUO $.
      $( [10-Nov-2002] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y S $.
    stcltr1.1 $e |- ( ph <-> ( S e. States /\ A. x e. CH A. y e. CH
                ( ( ( S ` x ) = 1 -> ( S ` y ) = 1 ) -> x C_ y ) ) ) $.
    stcltr1.2 $e |- A e. CH $.
    ${
      stcltr1.3 $e |- B e. CH $.
      $( Property of a strong classical state. $)
      stcltr1i $p |- ( ph ->
               ( ( ( S ` A ) = 1 -> ( S ` B ) = 1 ) -> A C_ B ) ) $=
        ( cst wcel cv cfv c1 wceq wi wss cch wral wa fveq2 eqeq1d imbi1d sseq1
        imbi12d imbi2d sseq2 rcla42v mp2an adantl sylbi ) AFJKZBLZFMZNOZCLZFMZN
        OZPZUMUPQZPZCRSBRSZTDFMZNOZEFMZNOZPZDEQZPZGVBVIULDRKERKVBVIPHIVAVIVDURP
        ZDUPQZPBCDERRUMDOZUSVJUTVKVLUOVDURVLUNVCNUMDFUAUBUCUMDUPUDUEUPEOZVJVGVK
        VHVMURVFVDVMUQVENUPEFUAUBUFUPEDUGUEUHUIUJUK $.
        $( [24-Oct-1999] $)
    $}

    $( Property of a strong classical state. $)
    stcltr2i $p |- ( ph -> ( ( S ` A ) = 1 -> A = ~H ) ) $=
      ( cfv c1 wceq chil wss wi helch stcltr1i ax-1 syl5 eqss chssii mpbiran
      syl6ibr ) ADEHIJZKDLZDKJZAKEHIJZUBMUCUBABCKDEFNGOUBUEPQUDDKLUCDKRDGSTUA
      $.
      $( [24-Oct-1999] $)

    ${
      stcltrlem1.3 $e |- B e. CH $.
      $( Lemma for strong classical state theorem. $)
      stcltrlem1 $p |- ( ph -> ( ( S ` B ) = 1 ->
                    ( S ` ( ( _|_ ` A ) vH ( A i^i B ) ) ) = 1 ) ) $=
        ( cfv c1 wceq cort cin chj co wa caddc cst wcel cv wi wss cch wral
        simplbi stji1i syl adantr chil stcltr2i imp ineq2 chm1i syl6eq fveq2d
        opreq2d cc cr choccli stcl mpi recnd addcom syl11anc sto1i eqtrd
        3eqtrd ex ) AEFJKLZDMJZDENZOPFJZKLAVJQZVMVKFJZVLFJZRPZVODFJZRPZKAVMVQLZ
        VJAFSTZVTAWABUAZFJKLCUAZFJKLUBWBWCUCUBCUDUEBUDUEGUFZDEFHIUGUHUIVNVPVRVO
        RVNVLDFVNEUJLZVLDLAVJWEABCEFGIUKULWEVLDUJNDEUJDUMDHUNUOUHUPUQVNWAVSKLAW
        AVJWDUIWAVSVRVORPZKWAVOURTVRURTVSWFLWAVOWAVKUDTVOUSTDHUTVKFVAVBVCWAVRWA
        DUDTVRUSTHDFVAVBVCVOVRVDVEDFHVFVGUHVHVI $.
        $( [24-Oct-1999] $)

      $( Lemma for strong classical state theorem. $)
      stcltrlem2 $p |- ( ph -> B C_ ( ( _|_ ` A ) vH ( A i^i B ) ) ) $=
        ( cfv c1 wceq cort cin chj co wi wss stcltrlem1 choccli chincli chjcli
        stcltr1i mpd ) AEFJKLDMJZDENZOPZFJKLQEUGRABCDEFGHISABCEUGFGIUEUFDHTDEHI
        UAUBUCUD $.
        $( [24-Oct-1999] $)
    $}
  $}

  ${
    $d x y s A $.  $d x y s B $.
    stcltrth.1 $e |- A e. CH $.
    stcltrth.2 $e |- B e. CH $.
    stcltrth.3 $e |- E. s e. States A. x e. CH A. y e. CH
                ( ( ( s ` x ) = 1 -> ( s ` y ) = 1 ) -> x C_ y ) $.
    $( Theorem for classically strong set of states.  If there exists a
       "classically strong set of states" on lattice ` CH ` (or actually any
       ortholattice, which would have an identical proof), then any two
       elements of the lattice commute, i.e., the lattice is distributive.
       (Proof due to Mladen Pavicic.)  Theorem 3.3 of [MegPav2000] p. 2344. $)
    stcltrthi $p |- B C_ ( ( _|_ ` A ) vH ( A i^i B ) ) $=
      ( cv cfv c1 wceq wi wss cch wral cst wrex cort cin chj co wcel wa biid
      stcltrlem2 r19.23aiva ax-mp ) AIZEIZJKLBIZUJJKLMUIUKNMBOPAOPZEQRDCSJCDTUA
      UBNZHULUMEQUJQUCULUDZABCDUJUNUEFGUFUGUH $.
      $( [24-Oct-1999] $)
  $}

