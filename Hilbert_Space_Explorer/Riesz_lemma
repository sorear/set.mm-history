$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Riesz lemma
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

    $( A continuous linear functional can be expressed as an inner product.
       Existence part of Theorem 3.9 of [Beran] p. 104. $)
    riesz3i $p |- E. w e. ~H A. v e. ~H ( T ` v ) = ( v .ih w ) $=
      ( cfv csp co wceq chil c0h c0v wcel wa cc0 cc adantr syl3anc syl2anc cmin
      vu cv wral wrex cort ax-hv0cl wf lnfnfi fveq2 nlelchi ococi choc0 3eqtr3g
      eleq2d biimpar elnlfn2 sylancr hi02 adantl eqtr4d ralrimiva opreq2 eqeq2d
      cnl ralbidv rcla4ev wne choccli chne0i cheli cdiv ccj csm ffvelrni anidms
      wi hicl his6 necon3bid divcl cjcl simpl hvmulcl adantll cmul sylan ancoms
      syl his2sub simpr ax-his3 opreq12d eqtr2d lnfnsubi lnfnmuli mulcom sylan2
      cmv hvsubcl eqtrd mulcl syl2an subid 3eqtrd wb elnlfn sylanbrc wss chssii
      ax-mp ocorth anassrs subeq0 mpbid adantlr jca divmul3 mpbird div23 simpll
      adantlll his52 eqtr3d ex mpdan rexlimiv sylbi pm2.61ine ) BUBZCFZYIAUBZGH
      ZIZBJUCZAJUDZCVDFZUEFZKYQKIZLJMYJYILGHZIZBJUCZYOUFYRYTBJYRYIJMZNZYJOYSUUC
      JPCUGZYIYPMZYJOICDUHZYRUUEUUBYRYPJYIYRYQUEFKUEFYPJYQKUEUIYPCDEUJZUKULUMUN
      UOYICUPUQUUBYSOIYRYIURUSUTVAYNUUAALJYKLIZYMYTBJUUHYLYSYJYKLYIGVBVCVEVFUQY
      QKVGUAUBZLVGZUAYQUDYOUAYQYPUUGVHZVIUUJYOUAYQUUIYQMZUUIJMZUUJYOVPUUIYQUUKV
      JUULUUMNZUUJYOUUNUUJNZUUICFZUUIUUIGHZVKHZVLFZUUIVMHZJMZYJYIUUTGHZIZBJUCZY
      OUUMUUJUVAUULUUMUUJNZUUSPMZUUMUVAUVEUURPMZUVFUVEUUPPMZUUQPMZUUQOVGZUVGUUM
      UVHUUJJPUUICUUFVNZQZUUMUVIUUJUUMUVIUUIUUIVQVOZQZUUMUVJUUJUUMUUQOUUILUUIVR
      VSUOZUUPUUQVTRZUURWAWHUUMUUJWBUUSUUIWCSWDUUOUVCBJUUOUUBNZUUPYIUUIGHZWEHZU
      UQVKHZYJUVBUVQUVTYJIZUVSYJUUQWEHZIZUUNUUBUWCUUJUUNUUBNZUVSUWBTHZOIZUWCUWD
      UWEUUPYIVMHZYJUUIVMHZWRHZUUIGHZOUUMUUBUWEUWJIUULUUMUUBNZUWJUWGUUIGHZUWHUU
      IGHZTHZUWEUWKUWGJMZUWHJMZUUMUWJUWNIUUMUVHUUBUWOUVKUUPYIWCWFZUUBUUMUWPUUBY
      JPMZUUMUWPJPYICUUFVNZYJUUIWCWFWGZUUMUUBWBZUWGUWHUUIWIRUWKUWLUVSUWMUWBTUWK
      UVHUUBUUMUWLUVSIUUMUVHUUBUVKQZUUMUUBWJUXAUUPYIUUIWKRUWKUWRUUMUUMUWMUWBIUU
      BUWRUUMUWSUSUXAUXAYJUUIUUIWKRWLWMWDUULUUMUUBUWJOIZUWKUULUXCUWKUWIYPMZUULU
      XCUWKUWIJMZUWICFZOIZUXDUWKUWOUWPUXEUWQUWTUWGUWHWSSUWKUXFUWGCFZUWHCFZTHZUU
      PYJWEHZUXKTHZOUWKUWOUWPUXFUXJIUWQUWTUWGUWHCDWNSUWKUXHUXKUXIUXKTUUMUVHUUBU
      XHUXKIUVKUUPYICDWOWFUUBUUMUXIUXKIZUUBUWRUUMUXMUWSUWRUUMNUXIYJUUPWEHZUXKYJ
      UUICDWOUUMUWRUVHUXNUXKIUVKYJUUPWPWQWTWFWGWLUWKUXKPMZUXLOIUUMUVHUWRUXOUUBU
      VKUWSUUPYJXAXBUXKXCWHXDUUDUXDUXEUXGNXEUUFUWICXFXJXGYPJXHUXDUULNUXCVPYPUUG
      XIUWIUUIYPXKXJWFWGXLWTUUMUUBUWFUWCXEZUULUWKUVSPMZUWBPMZUXPUWKUVHUVRPMZUXQ
      UXBUUBUUMUXSYIUUIVQWGZUUPUVRXASZUUBUUMUXRUUBUWRUVIUXRUUMUWSUVMYJUUQXAXBWG
      UVSUWBXMSWDXNXOUUMUUJUUBUWAUWCXEZUULUVEUUBNZUXQUWRUVIUVJNZUYBUUMUUBUXQUUJ
      UYAXOUUBUWRUVEUWSUSUVEUYDUUBUVEUVIUVJUVNUVOXPQZUVSYJUUQXQRYAXRUUMUUJUUBUV
      TUVBIUULUYCUVTUURUVRWEHZUVBUYCUVHUXSUYDUVTUYFIUVEUVHUUBUVLQUUMUUBUXSUUJUX
      TXOUYEUUPUVRUUQXSRUYCUVGUUBUUMUVBUYFIUVEUVGUUBUVPQUVEUUBWJUUMUUJUUBXTUURY
      IUUIYBRUTYAYCVAYNUVDAUUTJYKUUTIZYMUVCBJUYGYLUVBYJYKUUTYIGVBVCVEVFSYDYEYFY
      GYH $.
      $( [20-Nov-2013] $) $( [13-Feb-2006] $)

    $( A continuous linear functional can be expressed as an inner product.
       Uniqueness part of Theorem 3.9 of [Beran] p. 104. $)
    riesz4i $p |- E! w e. ~H A. v e. ~H ( T ` v ) = ( v .ih w ) $=
      ( vu cv cfv csp co wceq chil wral wa wi cmin cc0 wcel cc syl wreu ralbidv
      wrex opreq2 eqeq2d reu4 riesz3i r19.26 opreq12 adantl lnfnfi subid adantr
      ffvelrni eqtr3d ralimiaa sylbir cmv hvsubcl opreq1 opreq12d eqeq1d rcla4v
      cno c2 c0v wb normcl recnd sqeq0 norm-i bitrd normsq simpl simpr his2sub2
      cexp syl3anc eqtrd hvsubeq0 3bitr3d sylibd syl5 rgen2a mpbir2an ) BGZCHZW
      FAGZIJZKZBLMZALUAWKALUCWKWGWFFGZIJZKZBLMZNZWHWLKZOZFLMALMWKWOAFLWQWJWNBLW
      QWIWMWGWHWLWFIUDUEUBUFABCDEUGWRAFLWPWIWMPJZQKZBLMZWHLRZWLLRZNZWQWPWJWNNZB
      LMXAWJWNBLUHXEWTBLWFLRZXENWGWGPJZWSQXEXGWSKXFWGWIWGWMPUIUJXFXGQKZXEXFWGSR
      XHLSWFCCDUKUNWGULTUMUOUPUQXDXAWHWLURJZWHIJZXIWLIJZPJZQKZWQXDXILRZXAXMOWHW
      LUSZWTXMBXILWFXIKZWSXLQXPWIXJWMXKPWFXIWHIUTWFXIWLIUTVAVBVCTXDXIVDHZVEVQJZ
      QKZXIVFKZXMWQXDXNXSXTVGXOXNXSXQQKZXTXNXQSRXSYAVGXNXQXIVHVIXQVJTXIVKVLTXDX
      RXLQXDXRXIXIIJZXLXDXNXRYBKXOXIVMTXDXNXBXCYBXLKXOXBXCVNXBXCVOXIWHWLVPVRVSV
      BWHWLVTWAWBWCWDWE $.
      $( [13-Feb-2006] $)
  $}

  ${
    $d w v T $.
    $( A continuous linear functional can be expressed as an inner product.
       Uniqueness part of Theorem 3.9 of [Beran] p. 104.  See ~ riesz2 for the
       bounded linear functional version. $)
    riesz4 $p |- ( T e. ( LinFn i^i ConFn ) ->
                   E! w e. ~H A. v e. ~H ( T ` v ) = ( v .ih w ) ) $=
      ( clf ccnf cin wcel cv cfv csp wceq chil wral wreu cc0 csn cxp cif sselii
      co fveq1 eqeq1d ralbidv reubidv inss1 0lnfn 0cnfn mpbir2an elimel riesz4i
      elin inss2 dedth ) CDEFZGZBHZCIZUPAHJTZKZBLMZALNUPUOCLOPQZRZIZURKZBLMZALN
      CVACVBKZUTVEALVFUSVDBLVFUQVCURUPCVBUAUBUCUDABVBUNDVBDEUECVAUNVAUNGVADGVAE
      GVADEUKUFUGUHUIZSUNEVBDEULVGSUJUM $.
      $( [16-Feb-2006] $)
  $}

  ${
    $d x y z T $.
    $( Part 1 of the Riesz representation theorem for bounded linear
       functionals.  A linear functional is bounded iff its value can be
       expressed as an inner product.  Part of Theorem 17.3 of [Halmos] p. 31.
       For part 2, see ~ riesz2 .  For the continuous linear functional
       version, see ~ riesz3i and ~ riesz4 . $)
    riesz1 $p |- ( T e. LinFn -> ( ( normfn ` T ) e. RR <->
                  E. y e. ~H A. x e. ~H ( T ` x ) = ( x .ih y ) ) ) $=
      ( vz clf wcel ccnf cfv cr cv co wceq chil wral wrex wa cabs cmul cle wbr
      cnmf csp lnfncnbd cin cc0 csn cxp cif fveq1 eqeq1d rexralbidv inss1 0lnfn
      elin 0cnfn mpbir2an elimel sselii inss2 riesz3i dedth sylbir ex cno fveq2
      wi adantl bcs normcl cc recn mulcom syl2an breqtrd adantll adantr eqbrtrd
      an32s jctild opreq1 breq2d ralbidv rcla4ev syl6 rexlimdva lnfncon sylibrd
      ralimdva impbid bitr3d ) CEFZCGFZCUAHIFAJZCHZWMBJZUBKZLZAMNZBMOZCUCWKWLWS
      WKWLWSWKWLPCEGUDZFZWSCEGUNXAWSWMXACMUEUFUGZUHZHZWPLZAMNBMOCXBCXCLZWQXEBAM
      MXFWNXDWPWMCXCUIUJUKBAXCWTEXCEGULCXBWTXBWTFXBEFXBGFXBEGUNUMUOUPUQZURWTGXC
      EGUSXGURUTVAVBVCWKWSWNQHZDJZWMVDHZRKZSTZAMNZDIOZWLWKWRXNBMWKWOMFZPZWRWOVD
      HZIFZXHXQXJRKZSTZAMNZPXNXPWRYAXRXPWQXTAMWKWMMFZXOWQXTVFWKYBPXOPZWQXTYCWQP
      XHWPQHZXSSWQXHYDLYCWNWPQVEVGYCYDXSSTZWQYBXOYEWKYBXOPYDXJXQRKZXSSWMWOVHYBX
      JIFZXRYFXSLZXOWMVIWOVIZYGXJVJFXQVJFYHXRXJVKXQVKXJXQVLVMVMVNVOVPVQVCVRWHXO
      XRWKYIVGVSXMYADXQIXIXQLZXLXTAMYJXKXSXHSXIXQXJRVTWAWBWCWDWEDACWFWGWIWJ $.
      $( [25-Apr-2006] $)

    $( Part 2 of the Riesz representation theorem for bounded linear
       functionals.  The value of a bounded linear functional corresponds to a
       unique inner product.  Part of Theorem 17.3 of [Halmos] p. 31.  For part
       1, see ~ riesz1 . $)
    riesz2 $p |- ( ( T e. LinFn /\ ( normfn ` T ) e. RR ) ->
                   E! y e. ~H A. x e. ~H ( T ` x ) = ( x .ih y ) ) $=
      ( clf wcel cnmf cfv cr wa ccnf cin cv csp co wceq chil wral wreu lnfncnbd
      elin pm5.32i bitri riesz4 sylbir ) CDEZCFGHEZIZCDJKEZALZCGUIBLMNOAPQBPRUH
      UECJEZIUGCDJTUEUJUFCSUAUBBACUCUD $.
      $( [25-Apr-2006] $)
  $}

