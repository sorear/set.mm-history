$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Modular symmetry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d q r C $.
    $d c p q r A $.  $d c p q r B $.
    mdsymlem1.1 $e |- A e. CH $.
    mdsymlem1.2 $e |- B e. CH $.
    mdsymlem1.3 $e |- C = ( A vH p ) $.
    $( Lemma for ~ mdsym . $)
    mdsymlem1 $p |- ( ( ( p e. CH /\ ( B i^i C ) (_ A ) /\
            ( B MH* A /\ p (_ ( A vH B ) ) ) -> p (_ A ) $=
      ( cv cch wcel cin wss wa cdmd wbr chj co chub2t mpan2 syl6ssr chjcom
      sseq2i biimp anim12i ssin sylib ad2ant2rl wceq dmdit mp3anl1 mpanl1
      chjclt mpan syl5eqel adantr chub1t anim2i ancoms sylanc adantlr wb
      chinclt chlejb1t 3syl biimpa incom opreq1i syl5eq eqtr3d adantrr sseqtrd
      ) DHZIJZBCKZALZMZBANOZVLABPQZLZMMVLCBAPQZKZAVMVSVLWALZVOVQVMVSMVLCLZVLVTL
      ZMWBVMWCVSWDVMVLAVLPQZCVMAIJZVLWELEVLARSGTVSWDVRVTVLABEFUAUBUCUDVLCVTUEUF
      UGVPVQWAAUHVSVPVQMCBKZAPQZWAAVMVQWHWAUHZVOCIJZVQACLZMZWIVMVQMWFWJWLWIEBIJ
      ZWFWJWLWIFBACUIUJUKVMWJVQVMWEICWFVMWEIJEAVLULUMGUNZUOVQVMWLVMWKVQVMAWECWF
      VMAWELEAVLUPUMGTUQURUSUTVPWHAUHVQVPVNAPQZAWHVMVOWOAUHZVMWJVNIJZVOWPVAZWNW
      MWJWQFBCVBUMWQWFWREVNAVCSVDVEWGVNAPCBVFVGVHUOVIVJVK $.
      $( [29-Jul-2007] $) $( [1-Jul-2004] $)

    $( Lemma for ~ mdsym . $)
    mdsymlem2 $p |- ( ( ( p e. Atoms /\ ( B i^i C ) (_ A ) /\
         ( B MH* A /\ p (_ ( A vH B ) ) ) -> ( B =/= 0H -> E. r e. Atoms
         E. q e. Atoms ( p (_ ( q vH r ) /\ ( q (_ A /\ r (_ B ) ) ) ) $=
      ( cv cat wcel cin wss wa cdmd wbr chj co wrex c0h wne wceq opreq1 sseq2d
      sseq1 anbi1d anbi12d rcla4ev pm3.26 ad2antrr cch chub1t atelch syl2an
      adantlr mdsymlem1 sylanl1 adantr jca anim1i anass sylib anasss sylanc
      exp32 r19.22dv hatomic syl5 ) FJZKLZBCMANZOZBAPQVJABRSNOZOZDJZBNZDKTVJEJZ
      VPRSZNZVRANZVQOZOZEKTZDKTBUAUBVOVQWDDKVOVPKLZVQWDVKVJVJVPRSZNZVJANZVQOZOZ
      WDVOWEVQOZOWCWJEVJKVRVJUCZVTWGWBWIWLVSWFVJVRVJVPRUDUEWLWAWHVQVRVJAUFUGUHU
      IVMVKVNWKVKVLUJUKVOWEVQWJVOWEOZVQOWGWHOZVQOWJWMWNVQWMWGWHVMWEWGVNVKWEWGVL
      VJULLZVPULLWGVKWEVJVPUMVJUNZVPUNUOUPUPVOWHWEWOVLVNWHVKABCFGHIUQWPURUSUTVA
      WGWHVQVBVCVDVEVFVGDBHVHVI $.
      $( [29-Jul-2007] $) $( [1-Jul-2004] $)

    $( Lemma for ~ mdsym . $)
    mdsymlem3 $p |- ( ( ( ( p e. Atoms /\ -. ( B i^i C ) (_ A ) /\
               p (_ ( A vH B ) ) /\ A =/= 0H ) ->
   E. r e. Atoms E. q e. Atoms ( p (_ ( q vH r ) /\ ( q (_ A /\ r (_ B ) ) ) $=
      ( cv cat wcel cin wss wn wa chj co c0h wne wrex cch wb atelch chjclt
      mpan syl5eqel chinclt syl chrelat2t mpan2 3syl biimpa ad2antrr wi
      atcvat4 exp4b com34 com23 imp4b ssin sseq2i biimp adantl sylbir sylan2
      adantrr com12 adantlr imp wceq atnem0 ancoms sseq1 biimpcd con3d adantrl
      syl5bi adantll adantr chjcomt syl2an sseq2d atexcht syl3an1 3com13 3expa
      exp3a sylbid syld exp31 com24 imp3a anasss simprl a1i pm3.26 ad2antrl
      jctird jcad r19.22dv mpd exp32 ) FJZKLZBCMZANOZPZXDABQRNZPZASTZPZDJZXFNZX
      MANZOZPZDKUAZXDEJZXMQRNZXSANZXMBNZPZPZEKUAZDKUAXHXRXIXKXEXGXRXEXDUBLZXFUB
      LZXGXRUCZXDUDZYFCUBLZYGYFAXDQRZUBCAUBLZYFYKUBLGAXDUEUFIUGBUBLYJYGHBCUHUFU
      IYGYLYHGDXFAUJUKULUMUNXLXQYEDKXLXMKLZXQYEXLYMXQPZPZYAXMXDXSQRZNZPZEKUAZYE
      XLYNYSXHXKYNYSUOZXIXEXKYTXGYNXEXKPZYSYMXNUUAYSUOZXPYMXMYKNZUUBXNYMUUCXEXK
      YSYMXEUUCXKYSUOYMXEXKUUCYSYMXEXKUUCYSEAXMXDGUPUQURUSUTXNYBXMCNZPZUUCXMBCV
      AZUUDUUCYBUUDUUCCYKXMIVBVCVDVEVFVGVHVIVIVJYOYRYDEKXJYNXSKLZYRYDUOUOZXKXHY
      NUUHXIXEYNUUHXGXEYNPZUUGYRYDUUIUUGYRPZXTYCXEYMXQUUJXTUOXEYMPZXQUUGYRXTUUK
      YRUUGXQXTUUKYAYQUUGXQXTUOZUOUUKUUGYQYAUULUUKUUGYQYAUULUOUUKUUGPZYQPZYAXQX
      TUUNYAXQPZXSXMMSVKZXTUUMUUOUUPUOZYQYMUUGUUQXEYMUUGPXSXMVKZOZUUPUUOUUGYMUU
      SUUPUCXSXMVLVMYAXPUUSXNYAXPUUSYAUURXOUURYAXOXSXMAVNVOVPVJVQVRVSVTUUMYQUUP
      XTUOZUUMYQXMXSXDQRZNZUUTUUMYPUVAXMXEUUGYPUVAVKZYMYFXSUBLZUVCXEUUGXDXSWAYI
      XSUDZWBVIWCUUMUVBUUPXTXEYMUUGUVBUUPPXTUOZUUGYMXEUVFUVDYMXEUVFUUGXSXMXDWDU
      VEWEWFWGWHWIVJWJWHWKWLWMWLUTWNUUIUUJYAYBUUJYAUOUUIUUGYAYQWOWPYNYBXEXNYBYM
      XPXNUUEYBUUFYBUUDWQVEWRVDWSWTWHVIVIVIXAXBXCXAXB $.
      $( [18-May-2007] $) $( [2-Jul-2004] $)

    $( Lemma for ~ mdsym .  This is the forward direction of Lemma 4(i) of
       [Maeda] p. 168. $)
    mdsymlem4 $p |- ( p e. Atoms -> ( ( B MH* A /\
            ( ( A =/= 0H /\ B =/= 0H ) /\ p (_ ( A vH B ) ) ) -> E. q e. Atoms
               E. r e. Atoms ( p (_ ( q vH r ) /\ ( q (_ A /\ r (_ B ) ) ) ) $=
      ( cv cat wcel cdmd wbr c0h wne wa chj co wss wrex cin wi mdsymlem2 exp31
      com4t ex com23 a1d imp44 com3l wn mdsymlem3 anasss com3r ancoms adantlr
      adantl pm2.61d rexcom syl6ib ) FJZKLZBAMNZAOPZBOPZQVBABRSTZQZQZVBEJZDJZRS
      TVJATVKBTQQZEKUADKUAZVLDKUAEKUAVCBCUBATZVIVMUCVIVCVNVMVDVEVFVGVCVNVMUCUCZ
      VDVFVGVOUCUCVEVDVGVFVOVDVGVFVOUCVCVNVDVGQZVFVMVCVNVPVFVMUCABCDEFGHIUDUEUF
      UGUHUIUJUKVIVCVNULZVMVHVCVQVMUCUCZVDVEVGVRVFVGVEVRVCVQVGVEQZVMVCVQVSVMVCV
      QQVGVEVMABCDEFGHIUMUNUEUOUPUQURUKUSVLDEKKUTVA $.
      $( [30-Jul-2007] $) $( [2-Jul-2004] $)

    $( Lemma for ~ mdsym . $)
    mdsymlem5 $p |- ( ( q e. Atoms /\ r e. Atoms ) -> ( -. q = p ->
                    ( ( p (_ ( q vH r ) /\ ( q (_ A /\ r (_ B ) ) ->
                    ( ( ( c e. CH /\ A (_ c ) /\ p e. Atoms ) ->
                    ( p (_ c -> p (_ ( ( c i^i B ) vH A ) ) ) ) ) ) $=
      ( cv cat wcel wa cch wss chj co wceq wn cin wi w3a c0h wb atnem0 anbi2d
      3adant3 atexcht atelch syl3an1 sylbid exp3a 3com23 3expa adantrl adantrd
      imp32 adantrr sstr chub2t mpan sylan2 chub1t mpan2 anim12i anandirs
      ancoms adantll chlubt chjclt syl3an3 adantr mpbid chlejb2t biimpa
      ad2ant2lr sseqtrd exp45 anasss anim1i syl2an adantlr pm3.26 ad2antlr
      syl7 imp44 sstrd pm3.27 adantl jca ssin sylib chlej1t ex chinclt syl3an2
      3comr sstr2 chlej2t mp3anl2 sylanl2 adantllr syl5com chjcomt ad2antrr
      sseq1d sylibrd syld ad2antrl exp32 sylan imp31 mpd exp4d com34 imp4c
      com24 ) EKZLMZDKZLMZNZGKZOMZAYDPZNFKZLMZNYGXSYAQRZPZXSAPZYABPZNZNZXSYGSTZ
      YGYDPZYGYDBUAZAQRZPZUBZYCYEYFYHYNYOYTUBUBZYCYEYHYFUUAYCYEYHYFUUAUBYCYEYHN
      ZNZYFYNYOYTUUCYFYNYONZNZYPYSUUCUUEYPNZNZYAYQPZYSUUGYAYDPZYLNUUHUUGUUIYLUU
      GYAXSYGQRZYDUUCUUEYAUUJPZYPUUCUUDUUKYFUUCYNYOUUKUUCYJYOUUKUBZYMYCYHYJUULU
      BZYEXTYBYHUUMXTYHYBUUMXTYHYBUCZYJYOUUKUUNYJYONZYJXSYGUAUDSZNZUUKXTYHUUOUU
      QUEYBXTYHNYOUUPYJXSYGUFUGUHXSOMZYHYBUUQUUKUBXTXSYGYAUIXSUJZUKULUMUNUOUPUQ
      URUPUSUUCYFUUDYPUUJYDPZUUCYFYKYPUUTUBZUUDXTUUBYFYKUVAUBUBZYBUURYGOMZYENZU
      VBXTUUBUURUVCYEUVBUURUVCNZYENZYFYKYPUUTUVFYFYKYPNZNNUUJYDAQRZYDUVFUVGUUJU
      VHPZYFUVFUVGNXSUVHPZYGUVHPZNZUVIYEUVGUVLUVEUVGYEUVLYKYPYEUVLYKYENUVJYPYEN
      UVKYKAUVHPZUVJYEXSAUVHUTAOMZYEUVMHAYDVAVBVCYPYDUVHPZUVKYEYGYDUVHUTYEUVNUV
      OHYDAVDVEVCVFVGVHVIUVFUVLUVIUEZUVGUURUVCYEUVPUURUVCUVHOMZUVPYEXSYGUVHVJYE
      UVNUVQHYDAVKVEVLUOVMVNUPYEYFUVHYDSZUVEUVGYEYFUVRUVNYEYFUVRUEHAYDVOVBVPVQV
      RVSVTUUSYHYEUVDYHUVCYEYGUJWAVHWBWCYMYKYJYOYKYLWDWEWFWGWHUUFYLUUCUUDYLYFYP
      YMYLYJYOYKYLWIWEWEWJWKYAYDBWLWMUUCUUEUUHYSUBZYPUUCUUDUVSYFUUCYNUVSYOUUCYJ
      YMUVSUUCYJNYKUVSYLUUCYJYKUVSYCYEYJYKUVSUBUBZYHUURYAOMZNZYEUVTYCUWBYENZYJY
      KUVSUWCYJYKNNUUHYIYRPZYSUWCYKUUHUWDUBYJUWCYKNZUUHYAXSQRZYQXSQRZPZUWDUWCUU
      HUWHUBZYKUURUWAYEUWIUWAYEUURUWIUWAYQOMZUURUWIYEUWAUWJUURUCUUHUWHYAYQXSWNW
      OYEBOMUWJIYDBWPVEZWQWRUOVMUWEUWHUWFYRPZUWDUWHUWGYRPZUWLUWEUWFUWGYRWSUURYE
      YKUWMUWAUURUWJYKUWMYEUURUVNUWJYKUWMHXSAYQWTXAUWKXBXCXDUWEYIUWFYRUWBYIUWFS
      YEYKXSYAXEXFXGXHXIUPYJUWDYSUBUWCYKYGYIYRWSXJXIXKXTUURYBUWAUUSYAUJVFXLUSXM
      USVTUSUPUSXNXKXOXKXPXQXR $.
      $( [1-Aug-2004] $) $( [2-Jul-2004] $)

    $( Lemma for ~ mdsym .  This is the converse direction of Lemma 4(i) of
       [Maeda] p. 168, and is based on the proof of Theorem 1(d) to (e) of
       [Maeda] p. 167. $)
    mdsymlem6 $p |- ( A. p e. Atoms ( p (_ ( A vH B ) ->
      E. q e. Atoms E. r e. Atoms ( p (_ ( q vH r ) /\ ( q (_ A /\ r (_ B ) ) )
                   -> B MH* A ) $=
      ( vc cv chj co wss wa cat wrex wi wral cin cch cdmd wbr wcel wceq
      mdsymlem5 sseq1 sstr2 chinclt mpan2 chub2t mpan syl syl5 syl6bi imp3a
      a1i com13 adantrr ad2ant2r adantll com12 exp3a pm2.61d2 r19.23aivv
      imim2d com34 imp4b chjcom sseq2i anbi2i ssin bitr syl5ibr ex r19.20dva
      wb chrelat3t chjcl chjclt sylanc adantr sylibrd com3r r19.21aiv dmdbr2
      mp2an sylibr ) FKZABLMZNZWIEKZDKZLMNZWLANZWMBNZOZOZDPQEPQZRZFPSZAJKZNZXBB
      ALMZTZXBBTZALMZNZRZJUASZBAUBUCZXAXIJUAXBUAUDZXCXAXHXLXCXAXHRXLXCOZXAWIXEN
      ZWIXGNZRZFPSZXHXMWTXPFPXMWIPUDZOZWTXPXSWTOWIXBNZWKOZXOXNXSWTXTWKXOXSWTWKX
      TXOXSWSXTXORZWKWSXSYBWRXSYBRZEDPPWLPUDWMPUDOWLWIUEZWRYCRABCDEFJGHIUFYDWRX
      SYBWRXSOYDYBWQXSYDYBRZWNWOXMYEWPXRWOXLYEXCXTYDWOXLOZXOYDYFXORRXTYDWOXLXOY
      DWOWIANZXLXORWLWIAUGYGAXGNZXOXLWIAXGUHXLXFUAUDZYHXLBUAUDZYIHXBBUIUJZAUAUD
      ZYIYHGAXFUKULUMUNUOUPUQURUSUTVAVBVCVDVEVBVFVGVHYAXTWIXDNZOXNWKYMXTWJXDWIA
      BGHVIVJVKWIXBXDVLVMVNVOVPXLXHXQVQZXCXEUAUDZXGUAUDZYNXLFXEXGVRXLXDUAUDYOBA
      HGVSXBXDUIUJXLYIYPYKYIYLYPGXFAVTUJUMWAWBWCVOWDWEYJYLXKXJVQHGJBAWFWGWH $.
      $( [30-Jul-2007] $) $( [2-Jul-2004] $)

    $( Lemma for ~ mdsym .  Lemma 4(i) of [Maeda] p. 168.  Note that Maeda's
       1965 definition of dual modular pair has reversed arguments compared to
       the later (1970) definition given in Remark 29.6 of [MaedaMaeda] p. 130,
       which is the one that we use. $)
    mdsymlem7 $p |- ( ( A =/= 0H /\ B =/= 0H ) ->
                    ( B MH* A <->
   A. p e. Atoms ( p (_ ( A vH B ) -> E. q e. Atoms E. r e. Atoms
            ( p (_ ( q vH r ) /\ ( q (_ A /\ r (_ B ) ) ) ) ) $=
      ( c0h wne wa cdmd wbr cv chj co wss cat wrex wi wral wcel mdsymlem4
      exp4d com13 r19.21adv mdsymlem6 impbid1 ) AJKBJKLZBAMNZFOZABPQRZULEOZDOZP
      QRUNARUOBRLLDSTESTZUAZFSUBUJUKUQFSULSUCZUKUJUQURUKUJUMUPABCDEFGHIUDUEUFUG
      ABCDEFGHIUHUI $.
      $( [30-Jul-2007] $) $( [3-Jul-2004] $)

    $( Lemma for ~ mdsym .  Lemma 4(ii) of [Maeda] p. 168. $)
    mdsymlem8 $p |- ( ( A =/= 0H /\ B =/= 0H ) -> ( B MH* A <-> A MH* B ) ) $=
      ( vq vr c0h wne wa cv chj co wss cat wrex wi wral cdmd wbr wb chjcom
      sseq2i wcel cch wceq chjcomt atelch syl2an sseq2d ancom a1i anbi12d
      2rexbiia rexcom bitr imbi12i ralbii mdsymlem7 eqid ancoms 3bitr4d ) AJKZB
      JKZLZDMZABNOZPZVHHMZIMZNOZPZVKAPZVLBPZLZLZIQRHQRZSZDQTZVHBANOZPZVHVLVKNOZ
      PZVPVOLZLZHQRIQRZSZDQTZBAUAUBABUAUBZWAWJUCVGVTWIDQVJWCVSWHVIWBVHABEFUDUEV
      SWGIQRHQRWHVRWGHIQQVKQUFZVLQUFZLZVNWEVQWFWNVMWDVHVKUGUFVLUGUFVMWDUHWLWMVK
      VLUIVKUJVLUJUKULVQWFUCWNVOVPUMUNUOUPWGHIQQUQURUSUTUNABCIHDEFGVAVFVEWKWJUC
      BABVHNOZHIDFEWOVBVAVCVD $.
      $( [30-Jul-2007] $) $( [3-Jul-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    mdsym.1 $e |- A e. CH $.
    mdsym.2 $e |- B e. CH $.
    $( M-symmetry of the Hilbert lattice.  Lemma 5 of [Maeda] p. 168. $)
    mdsym $p |- ( A MH B <-> B MH A ) $=
      ( vx cort cfv c0h wceq cmd wbr wb wss wa chil chssi fveq2 pjococ choc0
      3eqtr3g sseq2d mpbiri cch wcel ssmd1 mp3an12 ssmd2 jca pm5.1 3syl wo wn
      wne neanior cdmd cv chj co choccl eqid mdsymlem8 mddmdt mp2an 3bitr4g
      sylbir ecase3 ) BFGZHIZAFGZHIZABJKZBAJKZLZVHABMZVKVLNZVMVHVNAOMACPVHBOAVH
      VGFGHFGZBOVGHFQBDRSTUAUBVNVKVLAUCUDZBUCUDZVNVKCDABUEUFVQVRVNVLCDABUGUFUHV
      KVLUIZUJVJBAMZVOVMVJVTBOMBDPVJAOBVJVIFGVPAOVIHFQACRSTUAUBVTVKVLVRVQVTVKDC
      BAUGUFVRVQVTVLDCBAUEUFUHVSUJVHVJUKULVGHUMVIHUMNZVMVGHVIHUNWAVIVGUOKZVGVIU
      OKZVKVLVGVIVGEUPUQURZEBDUSACUSWDUTVAVQVRVKWBLCDABVBVCVRVQVLWCLDCBAVBVCVDV
      EVF $.
      $( [2-Aug-2004] $) $( [3-Jul-2004] $)
  $}

  $( M-symmetry of the Hilbert lattice.  Lemma 5 of [Maeda] p. 168. $)
  mdsymt $p |- ( ( A e. CH /\ B e. CH ) -> ( A MH B <-> B MH A ) ) $=
    ( cch wcel cmd wbr wb chil cif wceq breq1 breq2 bibi12d helch elimel mdsym
    dedth2h ) ACDZBCDZABEFZBAEFZGRAHIZBEFZBUBEFZGUBSBHIZEFZUEUBEFZGABHHAUBJTUCU
    AUDAUBBEKAUBBELMBUEJUCUFUDUGBUEUBELBUEUBEKMUBUEAHCNOBHCNOPQ $.
    $( [14-Jun-2006] $) $( [6-Jul-2004] $)

  $( Dual M-symmetry of the Hilbert lattice. $)
  dmdsymt $p |- ( ( A e. CH /\ B e. CH ) -> ( A MH* B <-> B MH* A ) ) $=
    ( cch wcel wa cort cfv cmd wbr cdmd wb mdsymt chocclt syl2an dmdmdt ancoms
    3bitr4d ) ACDZBCDZEAFGZBFGZHIZUATHIZABJIBAJIZTCDUACDUBUCKRSTUALAMBMNABOSRUD
    UCKBAOPQ $.
    $( [12-Aug-2007] $) $( [25-Jul-2007] $)

  $( Two Hilbert lattice elements have the dual modular pair property if the
     second is an atom. $)
  atdmd2 $p |- ( ( A e. CH /\ B e. Atoms ) -> A MH* B ) $=
    ( cch wcel cat wa cdmd wbr atdmd ancoms wb dmdsymt atelch sylan2 mpbird )
    ACDZBEDZFABGHZBAGHZQPSBAIJPBCDRSKQABLBMNO $.
    $( [?] $) $( [6-Jul-2004] $)

  ${
    $d x y z w A $.  $d x y z w B $.  $d y z w C $.
    sumdmdi.1 $e |- A e. CH $.
    sumdmdi.2 $e |- B e. CH $.
    $( If the subspace sum of two Hilbert lattice elements is closed, then the
       elements are a dual modular pair.  Remark in [MaedaMaeda] p. 139. $)
    sumdmdi $p |- ( ( A +H B ) = ( A vH B ) -> A MH* B ) $=
      ( vx vy vz vw cph co chj wceq cv wss cin wi cch wral cdmd wbr wcel wa
      ineq2 adantr cva wrex cmv csh chsh shsubcltOLD exp3a syl ssel2 syl7
      exp4a com23 imp41 adantlr wb chil w3a hvsubaddt ax-hvcom eqeq1d eqcom
      syl6bb 3adant1 bitrd 3com23 chelt chel syl3an 3expa eleq1 syl6bir imp
      mpbid pm3.27 jca exp31 r19.22dv r19.42v syl6ib r19.22dva elin ancom bitr
      anbi1i anass rexbii2 syl6ibr chshi shinclt mpan2 ad2antrr shselt sylibrd
      chsel syl5ib ex imp3a ssrdv adantl eqsstr3d chinclt chslejt ad2antrl
      sstrd exp32 r19.21aiv dmdbr2 mp2an sylibr ) ABIJZABKJZLZBEMZNZXQXOOZXQAOZ
      BKJZNZPZEQRZABSTZXPYCEQXPXQQUAZXRYBXPYFXRUBZUBZXSXTBIJZYAYHXSXQXNOZYIXPYJ
      XSLYGXNXOXQUCUDYGYJYINXPYGFYJYIYGFMZXQUAZYKXNUAZUBYKYIUAZYKYJUAYGYLYMYNYG
      YLYMYNPYGYLUBZYKGMZHMZUEJZLZHBUFZGAUFZYNYMYOUUAYTGXTUFZYNYOUUAYPXQUAZYTUB
      ZGAUFUUBYOYTUUDGAYOYPAUAZUBZYTUUCYSUBZHBUFUUDUUFYSUUGHBUUFYQBUAZYSUUGUUFU
      UHUBZYSUBZUUCYSUUJYKYQUGJZXQUAZUUCUUIUULYSYOUUHUULUUEYFXRYLUUHUULYFYLXRUU
      HUULPYFYLXRUUHUULYFYLYQXQUAZUULXRUUHUBYFXQUHUAZYLUUMUULPPXQUIZUUNYLUUMUUL
      YKYQXQUJUKULBXQYQUMUNUOUPUQURUDUUIYSUULUUCUSZUUIYSUUKYPLZUUPYOUUEUUHUUQYS
      USZYKUTUAZYPUTUAZYQUTUAZUURYOUUEUUHUUSUVAUUTUURUUSUVAUUTVAUUQYQYPUEJZYKLZ
      YSYKYQYPVBUVAUUTUVCYSUSUUSUVAUUTUBZUVCYRYKLYSUVDUVBYRYKYQYPVCVDYRYKVEVFVG
      VHVIYFYLUUSXRYKXQVJURYPACVKYQBDVKVLVMUUKYPXQVNVOVPVQUUIYSVRVSVTWAUUCYSHBW
      BWCWDYTUUDGXTAYPXTUAZYTUBUUEUUCUBZYTUBUUEUUDUBUVEUVFYTUVEUUCUUEUBUVFYPXQA
      WEUUCUUEWFWGWHUUEUUCYTWIWGWJWKYOXTUHUAZYNUUBUSZYFUVGXRYLYFUUNUVGUUOUUNAUH
      UAUVGACWLXQAWMWNULWOUVGBUHUAUVHBDWLGHXTBYKWPWNULWQGHABYKCDWRWSWTXAYKXQXNW
      EWSXBXCXDYFYIYANZXPXRYFXTQUAZUVIYFAQUAZUVJCXQAXEWNUVJBQUAZUVIDXTBXFWNULXG
      XHXIXJUVKUVLYEYDUSCDEABXKXLXM $.
      $( [25-Jul-2007] $) $( [12-Jul-2004] $)

    $( Commuting subspaces form a modular pair. $)
    cmmd $p |- ( A C_H B -> A MH B ) $=
      ( ccm wbr cort cfv cdmd cmd cph co chj wceq cmcm4 choccl osumcor2 sylbi
      sumdmdi syl cch wcel wb mddmdt mp2an sylibr ) ABEFZAGHZBGHZIFZABJFZUGUHUI
      KLUHUIMLNZUJUGUHUIEFULABCDOUHUIACPZBDPZQRUHUIUMUNSTAUAUBBUAUBUKUJUCCDABUD
      UEUF $.
      $( [13-Aug-2005] $) $( [16-Jan-2005] $)

    $( Commuting subspaces form a dual modular pair. $)
    cmdmd $p |- ( A C_H B -> A MH* B ) $=
      ( cort cfv ccm wbr cmd cdmd choccl cmmd cmcm4 cch wcel wb dmdmdt mp2an
      3imtr4 ) AEFZBEFZGHTUAIHZABGHABJHZTUAACKBDKLABCDMANOBNOUCUBPCDABQRS $.
      $( [24-May-2006] $) $( [25-Apr-2006] $)

    $( Lemma for ~ sumdmd .  The span of vector ` C ` not in the subspace sum
       is "trimmed off." $)
    sumdmdlem $p |- ( ( C e. H~ /\ -. C e. ( A +H B ) ) ->
                    ( ( B +H ( span ` { C } ) ) i^i A ) = ( B i^i A ) ) $=
      ( vy vz vw chil wcel cph co wn wa csn cspn cfv cin cv wi cva wceq wrex
      csh wb spansnsht chshi shselt mpan syl c0v cmv w3a hvsubaddt eqcom
      syl6bb chel elspansnclt syl3an 3expa eleq1 shsvs syl5cbi adantr sylbird
      exp32 com4r imp31 adantrr shscl elspansn5t ax-mp adantl mpdd opreq2
      ax-hvaddid sylan9eqr sylan eqeq2d adantll biimpac elin biimpr ancoms
      biimparc sylan2 anassrs ex ad2antrl mpd imp a1d com23 com4l imp4c exp4a
      exp3a r19.23advv sylbid imp4b syl5ib ssrdv wss shsub1t ssrin 3syl eqssd
      ) CIJZCABKLZJMZNZBCOPQZKLZARZBARZXKFXNXOXKFSZXMJZXPAJZNXPXOJZXPXNJXHXJXQX
      RXSXHXQXJXRXSTZXHXQXPGSZHSZUALZUBZHXLUCGBUCZXJXTTZXHXLUDJZXQYEUEZCUFZBUDJ
      ZYGYHBEUGZGHBXLXPUHUIUJXHYDYFGHBXLXHYABJZYBXLJZNZYDYFXRXHYNYDNZXJXSXRYOXH
      XJXSTXRYOXHXJXSXRYLYMYDXKXSTZYDXRYLYMYPYDXRYLYMYPTYDXRYLNZNZXKYMXSYRXKYMX
      STYRXKNZYMYBUKUBZXSYSYMYBXIJZYTYRXHYMUUATZXJYDYQXHUUBYQXHYMYDUUAYQXHYMYDU
      UATYQXHYMNZNYDXPYAULLZYBUBZUUAXRYLUUCUUEYDUEZXPIJZYAIJZYBIJZUUFXRYLUUCUUG
      UUHUUIUMUUEYCXPUBYDXPYAYBUNYCXPUOUPXPADUQYABEUQZCYBURUSUTYQUUEUUATUUCUUEU
      UDXIJUUAYQUUDYBXIVAABXPYAADUGZYKVBVCVDVEVFVGVHVIXKYMUUAYTTTYRXKYMUUAYTXIU
      DJXKYMUUANNYTTABUUKYKVJXICYBVKVLVFVMVNYRYMYTXSTZTXKYRUULYMYDYQUULYDYQYTXS
      YDYQYTNZNXPYAUBZXSUUMYDUUNYLYTYDUUNUEXRYLYTNYCYAXPUUHYTYCYAUBYLYTUUHYCYAU
      KUALYAYBUKYAUAVOYAVPVQUUJVRVSVTWAYQUUNXSTYDYTYQUUNXSXRYLUUNXSXRXPBJZXSYLU
      UNNUUOXRXSXSUUOXRNXPBAWBWCWDUUNUUOYLXPYABVAWEWFWGWHWIWJVFWKWLVDVNWHWMVFWN
      WOWPWMWNWQWRWSWMWTXPXMAWBXAXBXHXOXNXCZXJXHYGBXMXCZUUPYIYJYGUUQYKBXLXDUIBX
      MAXEXFVDXG $.
      $( [23-Dec-2004] $) $( [18-Dec-2004] $)

    $( Lemma for ~ sumdmd . $)
    sumdmdlem2 $p |- ( A. x e. Atoms ( ( x vH B ) i^i ( A vH B ) ) (_
                  ( ( ( x vH B ) i^i A ) vH B ) -> ( A +H B ) = ( A vH B ) ) $=
      ( vy cv chj co cin wss cat wral cph wa wceq wcel chil wi wn csn cspn cfv
      csh spansnsht chshi shsub2t mpan2 syl spansnid sseldd ad2antrl c0v wne
      spansnat df-ne sylan2br opreq1 ineq1d opreq1d sseq12d rcla4v wb cch
      spansnjt chjcomt spansncht sylan2 eqtrd mpan adantr sylibrd com12 exp3a
      imp ssid c0h sneq fveq2d spansn0 syl6eq opreq2d shs0 inss1 chub2 ssini
      eqssi chincl chjcom chabs1 eqtr mpbiri pm2.61d2 adantrr shsub2 sumdmdlem
      sseq1d adantl sstrd sseld elin syl5ibr mpand exp32 com34 pm2.18 syl8
      chjcl chel syl5 pm2.43d ssrdv chslej jctil eqss sylibr ) AGZCHIZBCHIZJZXR
      BJZCHIZKZALMZBCNIZXSKZXSYEKZOYEXSPYDYGYFYDFXSYEYDFGZXSQZYHYEQZYDYHRQZYIYJ
      SZYIYDYKYIYJTZYJSYJYDYKYMYIYJYDYKYMYLYDYKYMOZOZYHCYHUAZUBUCZNIZQZYIYJYKYS
      YDYMYKYQYRYHYKYQUDQZYQYRKZYHUEYTCUDQUUACEUFZYQCUGUHUIYHUJUKULYOYHYRXSJZQY
      JYSYIOYOUUCYEYHYOUUCYRBJZCHIZYEYDYKUUCUUEKZYMYDYKOYHUMPZUUFYDYKUUGTZUUFSY
      DYKUUHUUFYKUUHOZYDUUFUUIYDYQCHIZXSJZUUJBJZCHIZKZUUFUUIYQLQZYDUUNSYKYHUMUN
      UUOUUHYHUOYHUMUPUQYCUUNAYQLXQYQPZXTUUKYBUUMUUPXRUUJXSXQYQCHURZUSUUPYAUULC
      HUUPXRUUJBUUQUSUTVAVBUIYKUUFUUNVCUUHYKUUCUUKUUEUUMYKYRUUJXSCVDQZYKYRUUJPE
      UURYKOYRCYQHIZUUJCYHVEUURYQVDQUUSUUJPYKCYQVFYHVGVHVIVJZUSYKUUDUULCHYKYRUU
      JBUUTUSUTVAVKVLVMVNVOUUGUUFCCKCVPZUUGUUCCUUECUUGUUCCXSJZCUUGYRCXSUUGYRCVQ
      NICUUGYQVQCNUUGYQUMUAZUBUCVQUUGYPUVCUBYHUMVRVSVTWAWBCUUBWCWAZUSUVBCCXSWDC
      CXSUVACBEDWEWFWGWAUUGUUECBJZCHIZCUUGUUDUVECHUUGYRCBUVDUSUTUVFCUVEHICUVECC
      BEDWHEWICBEDWJWKZWAVAWLWMWNYNUUEYEKZYDYNUVHCYEKCBUUBBDUFWOYNUUECYEYNUUEUV
      FCYNUUDUVECHBCYHDEWPUTUVGWAWQWLWRWSWTYHYRXSXAXBXCXDXEYJXFXGYHXSBCDEXHXIXJ
      XKXLBCDEXMXNYEXSXOXP $.
      $( [23-Dec-2004] $) $( [15-Jan-2005] $)

    $( The subspace sum of two Hilbert lattice elements is closed iff the
       elements are a dual modular pair.  Theorem 2 of [Holland] p. 1519. $)
    sumdmd $p |- ( ( A +H B ) = ( A vH B ) <-> A MH* B ) $=
      ( vx cph co chj wceq cdmd wbr sumdmdi cv cin wss cat wral cch wcel wb
      dmdbr4 mp2an atelch imim1i r19.20i2 sylbi sumdmdlem2 syl impbi ) ABFGABHG
      ZIZABJKZABCDLULEMZBHGZUJNUNANBHGOZEPQZUKULUOERQZUPARSBRSULUQTCDEABUAUBUOU
      OERPUMPSUMRSUOUMUCUDUEUFEABCDUGUHUI $.
      $( [5-Aug-2007] $) $( [14-Dec-2004] $)

    $( Dual modular pair property in terms of atoms. $)
    dmdbr4at $p |- ( A MH* B <-> A. x e. Atoms
            ( ( x vH B ) i^i ( A vH B ) ) (_ ( ( ( x vH B ) i^i A ) vH B ) ) $=
      ( cdmd wbr cv chj co cin wss cat wral cch wcel wb dmdbr4 mp2an atelch
      imim1i r19.20i2 sylbi cph wceq sumdmdlem2 sumdmd sylib impbi ) BCFGZAHZCI
      JZBCIJZKULBKCIJLZAMNZUJUNAONZUOBOPCOPUJUPQDEABCRSUNUNAOMUKMPUKOPUNUKTUAUB
      UCUOBCUDJUMUEUJABCDEUFBCDEUGUHUI $.
      $( [?] $) $( [15-Jan-2005] $)

    $( Dual modular pair property in terms of atoms. $)
    dmdbr5at $p |- ( A MH* B <->
   A. x e. Atoms ( x (_ ( A vH B ) -> x (_ ( ( ( x vH B ) i^i A ) vH B ) ) ) $=
      ( cdmd wbr cv chj co wss cin wi cat wral wcel cch dmdi4 mp3an12 com12
      atelch syl5 a1dd r19.21aiv cph wceq wn wa chjcomt mpan syl ineq1d chjcom
      ineq2i syl6eqr adantr atabs2 imp sseq2i negbii sylan2b eqtr3d chjclt
      mpan2 chinclt chub2t 3syl eqsstrd ex biantrud pm4.83 syl6bb ralbiia
      sumdmdlem2 sylbi sumdmd sylib impbi wb chjcl chlubt mp3an23 chub2
      biantru syl5bb ssid biantrur ssin bitr biimpa inss1 jctil eqss sylibr
      sseq1d mp3an2 mpdan bitrd bitr4d pm5.74da ) BCFGZAHZBCIJZKZXBCIJZXCLZXEBL
      ZCIJZKZMZANOZXDXBXHKZMZANOXAXKXAXJANXAXBNPZXIXDXAXBQPZXIXNXOXAXIBQPZCQPZX
      OXAXIMDEBCXBRSTXBUAZUBUCUDXKBCUEJXCUFZXAXKXIANOXSXJXIANXNXJXJXDUGZXIMZUHX
      IXNYAXJXNXTXIXNXTUHZXFCXHYBCXBIJZCBIJZLZXFCXNYEXFUFXTXNYEXEYDLXFXNYCXEYDX
      NXOYCXEUFZXRXQXOYFECXBUIUJUKULXCYDXEBCDEUMZUNUOUPXNXBYDKZUGZYECUFZXTXNYIY
      JCBXBEDUQURXDYHXCYDXBYGUSUTVAVBXNCXHKZXTXNXEQPZXGQPZYKXNXOYLXRXOXQYLEXBCV
      CVDZUKYLXPYMDXEBVEVDZXQYMYKECXGVFUJZVGUPVHVIVJXDXIVKVLVMABCDEVNVOBCDEVPVQ
      VRXJXMANXNXOXJXMVSXRXOXDXIXLXOXDUHZXIXEXHKZXLYQXFXEXHYQXFXEKZXEXFKZUHXFXE
      UFYQYTYSXOXDYTXOXDXEXCKZYTXOXDCXCKZUHZUUAXDXOXQXCQPUUCUUAVSEBCDEVTXBCXCWA
      WBUUBXDCBEDWCWDWEUUAXEXEKZUUAUHYTUUDUUAXEWFWGXEXEXCWHWIVLWJXEXCWKWLXFXEWM
      WNWOXOXLYRVSXDXOXLXLYKUHZYRXOYKXLXOYLYMYKYNYOYPVGVJXOXHQPZUUEYRVSZXOYLYMU
      UFYNYOYMXQUUFEXGCVCVDVGXOXQUUFUUGEXBCXHWAWPWQWRUPWSWTUKVMWI $.
      $( [12-Aug-2007] $) $( [14-Jan-2005] $)

    $( Dual modular pair property in terms of atoms.  The modular law takes
       the form of the shearing identity. $)
    dmdbr6at $p |- ( A MH* B <-> A. x e. Atoms
            ( ( A vH B ) i^i x ) = ( ( ( ( x vH B ) i^i A ) vH B ) i^i x ) ) $=
      ( cdmd wbr chj co cv cin wceq cat wral cch wcel wb dmdbr3 mp2an wa
      chabs2t mpan2 ineq2d incom inass 3eqtr syl5reqr adantr ineq1 adantl
      eqtr4d r19.20ia sylbi atelch imim1i r19.20i2 syl wss wi df-ss biimp
      syl5eq sseq1d inss1 sseq1 mpbiri syl5cbi r19.20si dmdbr5at sylibr impbi
      ) BCFGZBCHIZAJZKZVNCHIZBKCHIZVNKZLZAMNZVLVSAONZVTVLVQVPVMKZLZAONZWABOPCOP
      ZVLWDQDEABCRSWCVSAOVNOPZWCTVOWBVNKZVRWFVOWGLWCWFVMVNVPKZKZVOWGWFWHVNVMWFW
      EWHVNLEVNCUAUBUCWIWHVMKVNWBKWGVMWHUDVNVPVMUEVNWBUDUFUGUHWCVRWGLWFVQWBVNUI
      UJUKULUMVSVSAOMVNMPWFVSVNUNUOUPUQVTVNVMURZVNVQURZUSZAMNVLVSWLAMWJVOVQURZW
      KVSWJVOVNVQWJVNVMKZVNVOWJWNVNLVNVMUTVAVMVNUDVBVCVSWMVRVQURVQVNVDVOVRVQVEV
      FVGVHABCDEVIVJVK $.
      $( [13-Feb-2008] $) $( [18-Jan-2005] $)

    $( Dual modular pair property in terms of atoms. $)
    dmdbr7at $p |- ( A MH* B <->
       A. x e. Atoms ( ( A vH B ) i^i x ) (_ ( ( ( x vH B ) i^i A ) vH B ) ) $=
      ( cdmd wbr chj co cv cin wss cat wral wceq dmdbr6at inss1 sseq1 mpbiri
      r19.20si sylbi wi sseqin2 biimp sseq1d biimpcd dmdbr5at sylibr impbi ) BC
      FGZBCHIZAJZKZULCHIBKCHIZLZAMNZUJUMUNULKZOZAMNUPABCDEPURUOAMURUOUQUNLUNULQ
      UMUQUNRSTUAUPULUKLZULUNLZUBZAMNUJUOVAAMUSUOUTUSUMULUNUSUMULOULUKUCUDUEUFT
      ABCDEUGUHUI $.
      $( [?] $) $( [18-Jan-2005] $)
  $}

  ${
    mdoc1.1 $e |- A e. CH $.
    $( Orthocomplements form a modular pair. $)
    mdoc1 $p |- A MH ( _|_ ` A ) $=
      ( cort cfv ccm wbr cmd cmid cmcm2i choccl cmmd ax-mp ) AACDZEFAMGFAABBABH
      IAMBABJKL $.
      $( [15-Jun-2006] $) $( [29-Apr-2006] $)

    $( Orthocomplements form a modular pair. $)
    mdoc2 $p |- ( _|_ ` A ) MH A $=
      ( cort cfv cmd choccl mdoc1 ococ breqtr ) ACDZJCDAEJABFGABHI $.
      $( [3-Oct-2006] $) $( [29-Apr-2006] $)

    $( Orthocomplements form a dual modular pair. $)
    dmdoc1 $p |- A MH* ( _|_ ` A ) $=
      ( cort cfv ccm wbr cdmd cmid cmcm2i choccl cmdmd ax-mp ) AACDZEFAMGFAABBA
      BHIAMBABJKL $.
      $( [6-Jun-2006] $) $( [29-Apr-2006] $)

    $( Orthocomplements form a dual modular pair. $)
    dmdoc2 $p |- ( _|_ ` A ) MH* A $=
      ( cort cfv cdmd choccl dmdoc1 ococ breqtr ) ACDZJCDAEJABFGABHI $.
      $( [15-Jun-2006] $) $( [29-Apr-2006] $)
  $}


  ${
    mdcompl.1 $e |- A e. CH $.
    mdcompl.2 $e |- B e. CH $.
    $( A condition equivalent to the modular pair property.  Part of proof of
       Theorem 1.14 of [MaedaMaeda] p. 4. $)
    mdcompl $p |- ( A MH B <-> ( A i^i ( _|_ ` ( A i^i B ) ) ) MH
                  ( B i^i ( _|_ ` ( A i^i B ) ) ) ) $=
      ( cin cort cfv cmd wbr cdmd wa wss chj co wb chincl mdoc1 dmdoc2 pm3.2i
      ssid chil chjcl chssi chjo sseqtr4 choccl mdslmd1 mp2an ) ABEZUIFGZHIZUJU
      IJIZKUIUILZABMNZUIUJMNZLZKABHIAUJEBUJEHIOUKULUIABCDPZQUIUQRSUMUPUITUNUAUO
      UNABCDUBUCUIUQUDUESUIUJABUQUIUQUFCDUGUH $.
      $( [15-Jun-2006] $) $( [29-Apr-2006] $)

    $( A condition equivalent to the dual modular pair property. $)
    dmdcompl $p |- ( A MH* B <-> ( A i^i ( _|_ ` ( A i^i B ) ) ) MH*
                  ( B i^i ( _|_ ` ( A i^i B ) ) ) ) $=
      ( cin cort cfv cmd wbr cdmd wa wss chj co wb chincl mdoc1 dmdoc2 pm3.2i
      ssid chil chjcl chssi chjo sseqtr4 choccl mdsldmd1 mp2an ) ABEZUIFGZHIZUJ
      UIJIZKUIUILZABMNZUIUJMNZLZKABJIAUJEBUJEJIOUKULUIABCDPZQUIUQRSUMUPUITUNUAU
      OUNABCDUBUCUIUQUDUESUIUJABUQUIUQUFCDUGUH $.
      $( [18-Jun-2006] $) $( [29-Apr-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    mddmdin0.1 $e |- A e. CH $.
    mddmdin0.2 $e |- B e. CH $.
    mddmdin0.3 $e |- A. x e. CH A. y e. CH ( ( x MH* y /\ ( x i^i y ) = 0H ) ->
                     x MH y ) $.
    $( If dual modular implies modular whenever meet is zero, then dual
       modular implies modular for arbitrary lattice elements.  This theorem is
       needed for the remark after Lemma 7 of [Holland] p. 1524 to hold. $)
    mddmdin0 $p |- ( A MH* B -> A MH B ) $=
      ( cin cort cfv cdmd wbr cmd c0h wceq inindir chincl chocin eqtr3 cv wa
      wi cch wral wcel choccl breq1 ineq1 eqeq1d anbi12d imbi12d breq2 ineq2
      rcla42v mp2an ax-mp mpan2 dmdcompl mdcompl 3imtr4 ) CCDHZIJZHZDVBHZKLZVCV
      DMLZCDKLCDMLVEVCVDHZNOZVFVAVBHVGNCDVBPVACDEFQZRSATZBTZKLZVJVKHZNOZUAZVJVK
      MLZUBZBUCUDAUCUDZVEVHUAZVFUBZGVCUCUEVDUCUEVRVTUBCVBEVAVIUFZQDVBFWAQVQVTVC
      VKKLZVCVKHZNOZUAZVCVKMLZUBABVCVDUCUCVJVCOZVOWEVPWFWGVLWBVNWDVJVCVKKUGWGVM
      WCNVJVCVKUHUIUJVJVCVKMUGUKVKVDOZWEVSWFVFWHWBVEWDVHVKVDVCKULWHWCVGNVKVDVCU
      MUIUJVKVDVCMULUKUNUOUPUQCDEFURCDEFUSUT $.
      $( [7-Oct-2006] $) $( [29-Apr-2006] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.
    cdjreu.1 $e |- A e. SH $.
    cdjreu.2 $e |- B e. SH $.
    $( A member of the sum of disjoint subspaces has a unique decomposition.
       Part of Lemma 5 of [Holland] p. 1520. $)
    cdjreu $p |- ( ( C e. ( A +H B ) /\ ( A i^i B ) = 0H ) ->
                 E! x e. A E. y e. B C = ( x +h y ) ) $=
      ( vz vw cph co wcel cin c0h wceq wa cv cva wrex wi wral wreu shsel biimp
      cmv wb chil hvaddsub4t shel anim12i syl2an an4s adantll eleq1 csh
      shsubcltOLD ax-mp ancoms syl5cbir adantl adantr jctild eleq2 elin
      syl5bbr ad2antrr sylibd c0v hvsubeq0t elch0 syl5bb ad2antlr sylbid
      eqtr2t syl5 ex r19.23advv reeanv syl5ibr r19.21aivv opreq1 eqeq2d
      rexbidv opreq2 cbvrexv syl6bb reu4 sylibr ) ECDJKLZCDMZNOZPEAQZBQZRKZOZBD
      SZACSZWPEHQZIQZRKZOZIDSZPZWLWROZTZHCUAACUAZPWPACUBWIWQWKXFWIWQABCDEFGUCUD
      WKXEAHCCWKWLCLZWRCLZPZXEWKXIPZWOXAPZIDSBDSXDXCXJXKXDBIDDXJWMDLZWSDLZPZXKX
      DTXJXNPZWNWTOZXDXKXOXPWLWRUEKZWSWMUEKZOZXDXIXNXPXSUFZWKXGXLXHXMXTWLUGLZWM
      UGLZPWRUGLZWSUGLZPXTXGXLPXHXMPWLWMWRWSUHXGYAXLYBWLCFUIZWMDGUIUJXHYCXMYDWR
      CFUIZWSDGUIUJUKULUMXOXSXQNLZXDXOXSXQCLZXQDLZPZYGXIXNXSYJTWKXIXNPXSYIYHXNX
      SYITXIXSYIXRDLZXNXQXRDUNXMXLYKDUOLXMXLPYKTGWSWMDUPUQURUSUTXIYHXNCUOLXIYHT
      FWLWRCUPUQVAVBUMWKYJYGUFXIXNWKXQWJLYGYJWJNXQVCXQCDVDVEVFVGXIYGXDUFZWKXNYA
      YCYLXGXHYAYCPXQVHOXDYGWLWRVIXQVJVKYEYFUKVLVGVMEWNWTVNVOVPVQWOXABIDDVRVSVP
      VTUJWPXBAHCXDWPEWRWMRKZOZBDSXBXDWOYNBDXDWNYMEWLWRWMRWAWBWCYNXABIDWMWSOYMW
      TEWMWSWRRWDWBWEWFWGWH $.
      $( [26-Jul-2005] $) $( [20-May-2005] $)
  $}

  ${
    $d x y z w A $.  $d x y z w v B $.
    cdj1.1 $e |- A e. SH $.
    cdj1.2 $e |- B e. SH $.
    $( Two ways to express " ` A ` and ` B ` are completely disjoint
       subspaces."  (1) => (2) in Lemma 5 of [Holland] p. 1520. $)
    cdj1 $p |- ( E. w e. RR ( 0 < w /\ A. y e. A A. v e. B
      ( ( normh ` y ) + ( normh ` v ) ) <_ ( w x. ( normh ` ( y +h v ) ) ) ) ->
                    E. x e. RR ( 0 < x /\ A. y e. A A. z e. B
                    ( ( normh ` y ) = 1 -> x <_ ( normh ` ( y -h z ) ) ) ) ) $=
      ( cc0 cv clt wbr cno cfv caddc co cva cmul cle wral wa c1 wceq cmv wi cr
      wrex wcel cdiv wne gt0ne0t rerecclt syldan adantrr recgt0t cneg csm 1re
      a1i chil shel cc ax1cn negcl hvmulclt mpan syl normclt adantl axaddrcl
      adantr axmulrcl hvsubclt syl2an sylan2 anassrs wb addge01t biimpa
      normge0t sylanc ad2antlr csh shmulcltOLD ax-mp fveq2 opreq2d opreq2
      fveq2d breq12d rcla4v imp ad2ant2lr opreq1 eqcoms ad2antll hvsubvalt
      adantll 3brtr4d letrd ex adantllr lediv1t mp3anl1 pm3.26 ad2antrr jca
      pm3.27 sylibd divcan3t recnt recnd syl3anc breqtrd exp43 com23 r19.21adv
      r19.20dva imp32 breq2 breq1 imbi2d 2ralbidv anbi12d rcla4ev syl6
      r19.23aiv ) JDKZLMZBKZNOZEKZNOZPQZYIYKYMRQZNOZSQZTMZEGUAZBFUAZUBZJAKZLMZY
      LUCUDZUUCYKCKZUEQZNOZTMZUFZCGUABFUAZUBZAUGUHZDUGYIUGUIZUUBUCYIUJQZUGUIZJU
      UOLMZUUEUUOUUHTMZUFZCGUAZBFUAZUBZUBZUUMUUNUUBUVCUUNUUBUBZUUPUVBUUNYJUUPUU
      AUUNYJYIJUKZUUPYIULZYIUMUNUOUVDUUQUVAUUNYJUUQUUAYIUPUOUUNYJUUAUVAUUNYJUUA
      UVAUFUUNYJUBZYTUUTBFUVGYKFUIZUBZYTUUSCGUVIUUFGUIZYTUUSUVIUVJYTUUEUURUVIUV
      JUBZYTUUEUBZUBUUOYIUUHSQZYIUJQZUUHTUVKUVLUUOUVNTMZUVKUVLUCUVMTMZUVOUUNUVH
      UVJUVLUVPUFYJUUNUVHUBZUVJUBZUVLUVPUVRUVLUBZUCUCUCUQZUUFURQZNOZPQZUVMUCUGU
      IZUVSUSUTUVRUWCUGUIZUVLUVRUWBUGUIZUWEUVJUWFUVQUVJUWAVAUIZUWFUVJUUFVAUIZUW
      GUUFGIVBZUVTVCUIZUWHUWGUCVDVEZUVTUUFVFVGVHZUWAVIVHZVJUWDUWFUWEUSUCUWBVKVG
      VHVLUVRUVMUGUIZUVLUUNUVHUVJUWNUUNUUHUGUIZUWNUVHUVJUBZYIUUHVMUWPUUGVAUIZUW
      OYKVAUIZUWHUWQUVHUVJYKUUFVNYKFHVBZUWIVOUUGVIVHZVPVQZVLUVJUCUWCTMZUVQUVLUW
      FJUWBTMZUXBUVJUWFUXCUXBUWDUWFUXCUXBVRUSUCUWBVSVGVTUWMUVJUWGUXCUWLUWAWAVHW
      BWCUVSYLUWBPQZYIYKUWARQZNOZSQZUWCUVMTUVJYTUXDUXGTMZUVQUUEUVJYTUXHUVJUWAGU
      IZYTUXHUFUWJUVJUXIUWKGWDUIUWJUVJUBUXIUFIUVTUUFGWEWFVGYSUXHEUWAGYMUWAUDZYO
      UXDYRUXGTUXJYNUWBYLPYMUWANWGWHUXJYQUXFYISUXJYPUXENYMUWAYKRWIWJWHWKWLVHWMW
      NUUEUWCUXDUDZUVRYTUXKUCYLUCYLUWBPWOWPWQUVRUVMUXGUDZUVLUVHUVJUXLUUNUWPUUHU
      XFYISUWPUUGUXENUWRUWHUUGUXEUDUVHUVJYKUUFWRUWSUWIVOWJWHWSVLWTXAXBXCUWNUUNU
      BYJUVPUVOVRZUVKUWDUWNUUNYJUXMUSUCUVMYIXDXEUVKUWNUUNUUNUVHUVJUWNYJUXAXCUVG
      UUNUVHUVJUUNYJXFXGXHUVGYJUVHUVJUUNYJXIXGWBXJWMUVKUVNUUHUDZUVLYIVCUIZUUHVC
      UIZUVEUXNUVKYIUUHXKUVGUXOUVHUVJUUNUXOYJYIXLVLXGUVHUVJUXPUVGUWPUUHUWTXMWSU
      VGUVEUVHUVJUVFXGXNVLXOXPXQXRXSXBXTXHXHXBUULUVBAUUOUGUUCUUOUDZUUDUUQUUKUVA
      UUCUUOJLYAUXQUUJUUSBCFGUXQUUIUURUUEUUCUUOUUHTYBYCYDYEYFYGYH $.
      $( [6-Oct-2006] $) $( [21-May-2005] $)

    $( A property of " ` A ` and ` B ` are completely disjoint subspaces."
       Part of Lemma 5 of [Holland] p. 1520. $)
    cdj3lem1 $p |- ( E. x e. RR ( 0 < x /\ A. y e. A A. z e. B
      ( ( normh ` y ) + ( normh ` z ) ) <_ ( x x. ( normh ` ( y +h z ) ) ) ) ->
                    ( A i^i B ) = 0H ) $=
      ( vw cc0 cv clt wbr cno cfv caddc co cva cmul cle wral wa cin c0h wceq
      cr wcel wss c0v wi c1 cneg csm elin cc ax1cn negcl csh shmulcltOLD ax-mp
      mpan anim2i sylbi fveq2 opreq1d opreq1 fveq2d opreq2d breq12d opreq2
      rcla42v syl adantl chil wb c2 normnegt normclt recnd 2timest eqtr4d
      hvnegidt norm0 syl6eq recnt mul01t sylan9eqr 2cn mul01 syl6eqr 0re 2re
      w3a 2pos lemul2t mpan2 mp3an23 normge0t biantrud bitr3d letri3t norm-it
      3bitr2d bitrd shincl shel sylan2 sylibd ex com23 imp elch0 syl6ibr ssrdv
      shle0t syl6ib adantld r19.23aiv ) IAJZKLZBJZMNZCJZMNZOPZXRXTYBQPZMNZRPZSL
      ZCETBDTZUADEUBZUCUDZAUEXRUEUFZYIYKXSYLYIYJUCUGZYKYLYIYMYLYIUAZHYJUCYNHJZY
      JUFZYOUHUDZYOUCUFYLYIYPYQUIYLYPYIYQYLYPYIYQUIYLYPUAYIYOMNZUJUKZYOULPZMNZO
      PZXRYOYTQPZMNZRPZSLZYQYPYIUUFUIZYLYPYODUFZYTEUFZUAZUUGYPUUHYOEUFZUAUUJYOD
      EUMUUKUUIUUHYSUNUFZUUKUUIUJUOUPEUQUFUULUUKUAUUIUIGYSYOEURUSUTVAVBYHUUFYRY
      COPZXRYOYBQPZMNZRPZSLBCYOYTDEXTYOUDZYDUUMYGUUPSUUQYAYRYCOXTYOMVCVDUUQYFUU
      OXRRUUQYEUUNMXTYOYBQVEVFVGVHYBYTUDZUUMUUBUUPUUESUURYCUUAYROYBYTMVCVGUURUU
      OUUDXRRUURUUNUUCMYBYTYOQVIVFVGVHVJVKVLYLYOVMUFZUUFYQVNYPYLUUSUAZUUFVOYRRP
      ZVOIRPZSLZYQUUTUUBUVAUUEUVBSUUSUUBUVAUDYLUUSUUBYRYROPZUVAUUSUUAYRYROYOVPV
      GUUSYRUNUFUVAUVDUDUUSYRYOVQZVRYRVSVKVTVLUUTUUEIUVBUUSYLUUEXRIRPZIUUSUUDIX
      RRUUSUUDUHMNIUUSUUCUHMYOWAVFWBWCVGYLXRUNUFUVFIUDXRWDXRWEVKWFVOWGWHWIVHUUS
      UVCYQVNYLUUSUVCYRISLZIYRSLZUAZYRIUDZYQUUSUVGUVCUVIUUSYRUEUFZUVGUVCVNZUVEU
      VKIUEUFZVOUEUFZUVLWJWKUVKUVMUVNWLIVOKLUVLWMYRIVOWNWOWPVKUUSUVHUVGYOWQWRWS
      UUSUVKUVJUVIVNZUVEUVKUVMUVOWJYRIWTWOVKYOXAXBVLXCYOYJDEFGXDZXEXFXGXHXIXJYO
      XKXLXMXHYJUQUFYMYKVNUVPYJXNUSXOXPXQ $.
      $( [23-May-2005] $) $( [23-May-2005] $)
  $}

  ${
    $d x y z w v u t h A $.  $d x y z w v u t h B $.  $d x y z w v u C $.
    $d x y z w D $.
    cdj3lem2.1 $e |- A e. SH $.
    cdj3lem2.2 $e |- B e. SH $.
    ${
      $d v u t h S $.
      cdj3lem2.3 $e |- S = { <. x , y >. | ( x e. ( A +H B ) /\
                     y = U. { z e. A | E. w e. B x = ( z +h w ) } ) } $.
      $( Lemma for ~ cdj3 .  Value of the first-component function ` S ` . $)
      cdj3lem2 $p |- ( ( C e. A /\ D e. B /\ ( A i^i B ) = 0H ) ->
                    ( S ` ( C +h D ) ) = C ) $=
        ( wcel cin c0h wceq w3a cva co cfv cv wrex crab cuni wa cph shsva
        eqeq1 rexbidv rabbisdv unieqd csh elisseti rabex uniex fvopab4 syl
        3adant3 eqid opreq2 eqeq2d rcla4ev mpan2 3ad2ant2 wreu wb opreq1
        reuuni2 3simp1 cdjreu sylan 3impa sylanc mpbid eqtrd ) GEMZHFMZEFNOPZQZ
        GHRSZITZVTCUAZDUAZRSZPZDFUBZCEUCZUDZGVPVQWAWHPZVRVPVQUEZVTEFUFSZMZWIEFG
        HJKUGZABVTAUAZWDPZDFUBZCEUCZUDWHWKIWNVTPZWQWGWRWPWFCEWRWOWEDFWNVTWDUHUI
        UJUKLWGWFCEEULJUMUNUOUPUQURVSVTGWCRSZPZDFUBZWHGPZVQVPXAVRVQVTVTPZXAVTUS
        WTXCDHFWCHPWSVTVTWCHGRUTVAVBVCVDVPWFCEVEZXAXBVFVSWFXACEGWBGPZWEWTDFXEWD
        WSVTWBGWCRVGVAUIVHVPVQVRVIVPVQVRXDWLVRXDWJCDEFVTJKVJWMVKVLVMVNVO $.
        $( [23-May-2005] $) $( [23-May-2005] $)

      $( Lemma for ~ cdj3 .  Closure of the first-component function ` S ` . $)
      cdj3lem2a $p |- ( ( C e. ( A +H B ) /\ ( A i^i B ) = 0H ) ->
                    ( S ` C ) e. A ) $=
        ( vv vu cin c0h wceq cph co wcel cfv cv cva wrex wa fveq2 eleq1d w3a
        cdj3lem2 3simp1 eqeltrd 3expa syl5bir exp3a com13 r19.23advv shsel
        syl5ib impcom ) EFNOPZGEFQRSZGHTZESZUSGLUAZMUAZUBRZPZMFUCLEUCVBUTUSVFVB
        LMEFVFVCESZVDFSZUDZUSVBVFVIUSVBVFVBVEHTZESZVIUSUDVFVAVJEGVEHUEUFVGVHUSV
        KVGVHUSUGVJVCEABCDEFVCVDHIJKUHVGVHUSUIUJUKULUMUNUOLMEFGIJUPUQUR $.
        $( [25-May-2005] $) $( [25-May-2005] $)

      $( Lemma for ~ cdj3 .  The first-component function ` S ` is bounded if
         the subspaces are completely disjoint. $)
      cdj3lem2b $p |- ( E. v e. RR ( 0 < v /\ A. x e. A A. y e. B
      ( ( normh ` x ) + ( normh ` y ) ) <_ ( v x. ( normh ` ( x +h y ) ) ) ) ->
                    E. v e. RR ( 0 < v /\ A. u e. ( A +H B )
                      ( normh ` ( S ` u ) ) <_ ( v x. ( normh ` u ) ) ) ) $=
        ( vt vh cin c0h wceq cc0 cv clt wbr cno cfv caddc co cva cmul cle wral
        wa cr wrex cph cdj3lem1 wcel wi wreu cdjreu reurex syl adantrr fveq2
        opreq1d opreq1 fveq2d opreq2d breq12d opreq2 rcla42v cdj3lem2 3expa
        ad2ant2r chil shel normge0t adantl wb addge01t normclt syl2an mpbid
        adantr letrt ad2antrr axaddrcl axmulrcl hvaddclt sylan2 ancoms syl3anc
        mpand imp an1rs adantrl eqbrtrd syl5cbir exp31 syld com14 com4t
        r19.23advv mpd ex com3l r19.21adv anim2d r19.22dva mpcom ) GHOPQZRESZTU
        AZASZUBUCZBSZUBUCZUDUEZXJXLXNUFUEZUBUCZUGUEZUHUAZBHUIAGUIZUJZEUKULXKFSZ
        IUCZUBUCZXJYCUBUCZUGUEZUHUAZFGHUMUEZUIZUJZEUKULEABGHJKUNXIYBYKEUKXIXJUK
        UOZUJZYAYJXKYMYAYHFYIYCYIUOZYMYAYHYNYMYAYHUPZYNYMUJYCMSZNSZUFUEZQZNHULZ
        MGULZYOYNXIUUAYLYNXIUJYTMGUQUUAMNGHYCJKURYTMGUSUTVAYMUUAYOUPYNYMYSYOMNG
        HYSYAYMYPGUOZYQHUOZUJZYHUUDYAYMYSYHUUDYAYPUBUCZYQUBUCZUDUEZXJYRUBUCZUGU
        EZUHUAZYMYSYHUPZUPXTUUJUUEXOUDUEZXJYPXNUFUEZUBUCZUGUEZUHUAABYPYQGHXLYPQ
        ZXPUULXSUUOUHUUPXMUUEXOUDXLYPUBVBVCUUPXRUUNXJUGUUPXQUUMUBXLYPXNUFVDVEVF
        VGXNYQQZUULUUGUUOUUIUHUUQXOUUFUUEUDXNYQUBVBVFUUQUUNUUHXJUGUUQUUMYRUBXNY
        QYPUFVHVEVFVGVIUUDUUJYMUUKYSYHYRIUCZUBUCZUUIUHUAUUDUUJUJZYMUJZYSYEUUSYG
        UUIUHYSYDUURUBYCYRIVBVEYSYFUUHXJUGYCYRUBVBVFVGUVAUUSUUEUUIUHUUDXIUUSUUE
        QUUJYLUUDXIUJUURYPUBUUBUUCXIUURYPQABCDGHYPYQIJKLVJVKVEVLUUTYLUUEUUIUHUA
        ZXIUUDYLUUJUVBUUDYLUJZUUJUVBUVCUUEUUGUHUAZUUJUVBUUDUVDYLUUDRUUFUHUAZUVD
        UUCUVEUUBUUCYQVMUOZUVEYQHKVNZYQVOUTVPUUEUKUOZUUFUKUOZUVEUVDVQUUBUUCUUEU
        UFVRUUBYPVMUOZUVHYPGJVNZYPVSUTZUUCUVFUVIUVGYQVSUTZVTWAWBUVHUUGUKUOZUUIU
        KUOZUVDUUJUJUVBUPUVCUUEUUGUUIWCUUBUVHUUCYLUVLWDUUDUVNYLUVHUVIUVNUUBUUCU
        UEUUFWEUVLUVMVTWBYLUUDUVOYLUUHUKUOZUVOUUDXJUUHWFUUDYRVMUOZUVPUVJUVFUVQU
        UBUUCYPYQWGUVKUVGVTYRVSUTWHWIWJWKWLWMWNWOWPWQWRWSWTXAVPXBXCXDXEXFXGXH
        $.
        $( [23-May-2005] $) $( [26-May-2005] $)
    $}

    ${
      $d v u t h T $.
      cdj3lem3.3 $e |- T = { <. x , y >. | ( x e. ( A +H B ) /\
                     y = U. { w e. B | E. z e. A x = ( z +h w ) } ) } $.
      $( Lemma for ~ cdj3 .  Value of the second-component function ` T ` . $)
      cdj3lem3 $p |- ( ( C e. A /\ D e. B /\ ( A i^i B ) = 0H ) ->
                    ( T ` ( C +h D ) ) = D ) $=
        ( wcel cin c0h wceq cva co cfv w3a wa chil ax-hvcom shel syl2an fveq2d
        3adant3 cv cph wrex crab cuni copab shscom eleq2i eqeq2d rexbidva
        rabbii unieqi eqeq2i anbi12i opabbii eqtr4 cdj3lem2 eqtr3d incom
        eqeq1i syl3an3b 3com12 ) HFMZGEMZEFNZOPZGHQRZISZHPZVJVKFENZOPZVPVMVJVKV
        RTHGQRZISZVOHVJVKVTVOPVRVJVKUAVSVNIHUBMGUBMVSVNPVJVKHGUCHFKUDGEJUDUEUFU
        GABDCFEHGIKJIAUHZEFUIRZMZBUHZWACUHZDUHZQRZPZCEUJZDFUKZULZPZUAZABUMWAFEU
        IRZMZWDWAWFWEQRZPZCEUJZDFUKZULZPZUAZABUMLXBWMABWOWCXAWLWNWBWAFEKJUNUOWT
        WKWDWSWJWRWIDFWFFMZWQWHCEXCWEEMZUAWPWGWAWFUBMWEUBMWPWGPXCXDWFWEUCWFFKUD
        WEEJUDUEUPUQURUSUTVAVBVCVDVEVLVQOEFVFVGVHVI $.
        $( [23-May-2005] $) $( [23-May-2005] $)

      $( Lemma for ~ cdj3 .  Closure of the second-component function
         ` T ` . $)
      cdj3lem3a $p |- ( ( C e. ( A +H B ) /\ ( A i^i B ) = 0H ) ->
                    ( T ` C ) e. B ) $=
        ( vv vu cin c0h wceq cph co wcel cfv cv cva wrex wa fveq2 eleq1d w3a
        cdj3lem3 3simp2 eqeltrd 3expa syl5bir exp3a com13 r19.23advv shsel
        syl5ib impcom ) EFNOPZGEFQRSZGHTZFSZUSGLUAZMUAZUBRZPZMFUCLEUCVBUTUSVFVB
        LMEFVFVCESZVDFSZUDZUSVBVFVIUSVBVFVBVEHTZFSZVIUSUDVFVAVJFGVEHUEUFVGVHUSV
        KVGVHUSUGVJVDFABCDEFVCVDHIJKUHVGVHUSUIUJUKULUMUNUOLMEFGIJUPUQUR $.
        $( [26-May-2005] $) $( [26-May-2005] $)

      $( Lemma for ~ cdj3 .  The second-component function ` T ` is bounded if
         the subspaces are completely disjoint. $)
      cdj3lem3b $p |- ( E. v e. RR ( 0 < v /\ A. x e. A A. y e. B
      ( ( normh ` x ) + ( normh ` y ) ) <_ ( v x. ( normh ` ( x +h y ) ) ) ) ->
                    E. v e. RR ( 0 < v /\ A. u e. ( A +H B )
                      ( normh ` ( T ` u ) ) <_ ( v x. ( normh ` u ) ) ) ) $=
        ( vt vh cc0 cv clt wbr cno cfv caddc co cva cmul cle wral wa cr wrex
        cph wcel wceq crab cuni copab shscom eleq2i chil ax-hvcom shel syl2an
        eqeq2d rexbidva rabbii unieqi eqeq2i anbi12i opabbii eqtr4 cdj3lem2b
        fveq2 opreq1d opreq1 fveq2d opreq2d breq12d opreq2 cbvral2v ralcom cc
        axaddcom normclt syl recnd ralbidva ralbiia bitr2 3bitr anbi2i rexbii
        wb raleq1 ax-mp 3imtr4 ) OEPZQRZAPZSTZBPZSTZUAUBZWOWQWSUCUBZSTZUDUBZUER
        ZBGUFZAHUFZUGZEUHUIWPFPZITSTWOXISTUDUBUERZFHGUJUBZUFZUGZEUHUIWPXEBHUFAG
        UFZUGZEUHUIWPXJFGHUJUBZUFZUGZEUHUIABDCEFHGIKJIWQXPUKZWSWQCPZDPZUCUBZULZ
        CGUIZDHUMZUNZULZUGZABUOWQXKUKZWSWQYAXTUCUBZULZCGUIZDHUMZUNZULZUGZABUOLY
        PYHABYIXSYOYGXKXPWQHGKJUPUQYNYFWSYMYEYLYDDHYAHUKZYKYCCGYQXTGUKZUGYJYBWQ
        YAURUKXTURUKYJYBULYQYRYAXTUSYAHKUTXTGJUTVAVBVCVDVEVFVGVHVIVJXOXHEUHXNXG
        WPXNMPZSTZNPZSTZUAUBZWOYSUUAUCUBZSTZUDUBZUERZNHUFMGUFUUGMGUFNHUFZXGXEUU
        GYTWTUAUBZWOYSWSUCUBZSTZUDUBZUERABMNGHWQYSULZXAUUIXDUULUEUUMWRYTWTUAWQY
        SSVKVLUUMXCUUKWOUDUUMXBUUJSWQYSWSUCVMVNVOVPWSUUAULZUUIUUCUULUUFUEUUNWTU
        UBYTUAWSUUASVKVOUUNUUKUUEWOUDUUNUUJUUDSWSUUAYSUCVQVNVOVPVRUUGMNGHVSXGWT
        WRUAUBZWOWSWQUCUBZSTZUDUBZUERZBGUFZAHUFUUHXFUUTAHWQHUKZXEUUSBGUVAWSGUKZ
        UGZXAUUOXDUURUEWRVTUKWTVTUKXAUUOULUVAUVBWRWTWAUVAWRUVAWQURUKZWRUHUKWQHK
        UTZWQWBWCWDUVBWTUVBWSURUKZWTUHUKWSGJUTZWSWBWCWDVAUVCXCUUQWOUDUVCXBUUPSU
        VDUVFXBUUPULUVAUVBWQWSUSUVEUVGVAVNVOVPWEWFUUSUUGWTUUBUAUBZWOWSUUAUCUBZS
        TZUDUBZUERABNMHGWQUUAULZUUOUVHUURUVKUEUVLWRUUBWTUAWQUUASVKVOUVLUUQUVJWO
        UDUVLUUPUVISWQUUAWSUCVQVNVOVPWSYSULZUVHUUCUVKUUFUEUVMWTYTUUBUAWSYSSVKVL
        UVMUVJUUEWOUDUVMUVIUUDSWSYSUUAUCVMVNVOVPVRWGWHWIWJXRXMEUHXQXLWPXPXKULXQ
        XLWKGHJKUPXJFXPXKWLWMWIWJWN $.
        $( [31-May-2005] $) $( [31-May-2005] $)
    $}
  $}

  ${
    $d x y z w v u t h f g A $.  $d x y z w v u t h f g B $.
    $d v u t h f g S $.  $d v u t h f g T $.
    cdj3.1 $e |- A e. SH $.
    cdj3.2 $e |- B e. SH $.
    cdj3.3 $e |- S = { <. x , y >. | ( x e. ( A +H B ) /\
                   y = U. { z e. A | E. w e. B x = ( z +h w ) } ) } $.
    cdj3.4 $e |- T = { <. x , y >. | ( x e. ( A +H B ) /\
                   y = U. { w e. B | E. z e. A x = ( z +h w ) } ) } $.
    cdj3.5 $e |- ( ph <-> E. v e. RR ( 0 < v /\ A. u e. ( A +H B )
                      ( normh ` ( S ` u ) ) <_ ( v x. ( normh ` u ) ) ) ) $.
    cdj3.6 $e |- ( ps <-> E. v e. RR ( 0 < v /\ A. u e. ( A +H B )
                      ( normh ` ( T ` u ) ) <_ ( v x. ( normh ` u ) ) ) ) $.
    $( Two ways to express " ` A ` and ` B ` are completely disjoint
       subspaces."  (1) <=> (3) in Lemma 5 of [Holland] p. 1520. $)
    cdj3 $p |- ( E. v e. RR ( 0 < v /\ A. x e. A A. y e. B
      ( ( normh ` x ) + ( normh ` y ) ) <_ ( v x. ( normh ` ( x +h y ) ) ) )
         <-> ( ( A i^i B ) = 0H /\ ph /\ ps ) ) $=
      ( vf vg vt vh cc0 cv clt wbr cno cfv caddc co cva cmul cle wral wa cr
      wrex cin c0h wceq w3a cdj3lem1 cph cdj3lem2b sylibr cdj3lem3b 3jca wcel
      wi axaddrcl breq2 opreq1 breq2d 2ralbidv fveq2 opreq1d fveq2d opreq2d
      breq12d opreq2 cbvral2v syl5bb anbi12d rcla4ev ex syl adantl addgt0t
      shsva rcla4v anim12d le2addt chil shel normclt anim12i axmulrcl hvaddclt
      syl2an sylan2 adantlr adantll jca sylanc wb cdj3lem2 breq1d cdj3lem3
      3expa ancoms cc adddirt recnt recnd syl3an 3imtr4d syld r19.21advva
      syl2and an4 syl5ib r19.23advv ralbidv cbvrexv bitr anbi12i reeanv bitr4
      3impib impbi ) UCGUDZUEUFZCUDZUGUHZDUDZUGUHZUIUJZYKYMYOUKUJZUGUHZULUJZUMU
      FZDJUNCIUNZUOZGUPUQZIJURUSUTZABVAUUDUUEABGCDIJMNVBUUDYLHUDZKUHZUGUHZYKUUF
      UGUHZULUJZUMUFZHIJVCUJZUNZUOZGUPUQZACDEFGHIJKMNOVDQVEUUDYLUUFLUHZUGUHZUUJ
      UMUFZHUULUNZUOZGUPUQZBCDEFGHIJLMNPVFRVEVGUUEABUUDUUEUCSUDZUEUFZUUHUVBUUIU
      LUJZUMUFZHUULUNZUOZUCTUDZUEUFZUUQUVHUUIULUJZUMUFZHUULUNZUOZUOZTUPUQSUPUQZ
      UUDABUOZUUEUVNUUDSTUPUPUUEUVBUPVHZUVHUPVHZUOZUVNUUDVIUUEUVSUOZUVCUVIUOZUV
      FUVLUOZUOUUDUVNUVTUCUVBUVHUIUJZUEUFZUAUDZUGUHZUBUDZUGUHZUIUJZUWCUWEUWGUKU
      JZUGUHZULUJZUMUFZUBJUNUAIUNZUUDUWAUWBUVSUWDUWNUOZUUDVIZUUEUVSUWCUPVHZUWPU
      VBUVHVJUWQUWOUUDUUCUWOGUWCUPYKUWCUTZYLUWDUUBUWNYKUWCUCUEVKUWRUWIYKUWKULUJ
      ZUMUFZUBJUNUAIUNUWNUUBUWRUWTUWMUAUBIJUWRUWSUWLUWIUMYKUWCUWKULVLVMVNUUAUWT
      UWFYPUIUJZYKUWEYOUKUJZUGUHZULUJZUMUFCDUAUBIJYMUWEUTZYQUXAYTUXDUMUXEYNUWFY
      PUIYMUWEUGVOVPUXEYSUXCYKULUXEYRUXBUGYMUWEYOUKVLVQVRVSYOUWGUTZUXAUWIUXDUWS
      UMUXFYPUWHUWFUIYOUWGUGVOVRUXFUXCUWKYKULUXFUXBUWJUGYOUWGUWEUKVTVQVRVSWAWBW
      CWDWEWFWGUVSUWAUWDVIUUEUVSUWAUWDUVBUVHWHWEWGUVTUWBUWMUAUBIJUVTUWEIVHZUWGJ
      VHZUOZUOZUWBUWJKUHZUGUHZUVBUWKULUJZUMUFZUWJLUHZUGUHZUVHUWKULUJZUMUFZUOZUW
      MUXIUWBUXSVIZUVTUXIUWJUULVHZUXTIJUWEUWGMNWIUYAUVFUXNUVLUXRUVEUXNHUWJUULUU
      FUWJUTZUUHUXLUVDUXMUMUYBUUGUXKUGUUFUWJKVOVQUYBUUIUWKUVBULUUFUWJUGVOZVRVSW
      JUVKUXRHUWJUULUYBUUQUXPUVJUXQUMUYBUUPUXOUGUUFUWJLVOVQUYBUUIUWKUVHULUYCVRV
      SWJWKWFWGUXJUWFUXMUMUFZUWHUXQUMUFZUOZUWIUXMUXQUIUJZUMUFZUXSUWMUVSUXIUYFUY
      HVIZUUEUWFUPVHZUWHUPVHZUOZUXMUPVHZUXQUPVHZUOUYIUVSUXIUOZUWFUWHUXMUXQWLUXI
      UYLUVSUXGUYJUXHUYKUXGUWEWMVHZUYJUWEIMWNZUWEWOWFUXHUWGWMVHZUYKUWGJNWNZUWGW
      OWFWPWGUYOUYMUYNUVQUXIUYMUVRUVQUWKUPVHZUYMUXIUVBUWKWQUXIUWJWMVHZUYTUYPUYR
      VUAUXGUXHUWEUWGWRUYQUYSWSUWJWOWFZWTXAUVRUXIUYNUVQUVRUYTUYNUXIUVHUWKWQVUBW
      TXBXCXDXBUUEUXIUXSUYFXEZUVSUXIUUEVUCUXGUXHUUEVUCUXGUXHUUEVAZUXNUYDUXRUYEV
      UDUXLUWFUXMUMVUDUXKUWEUGCDEFIJUWEUWGKMNOXFVQXGVUDUXPUWHUXQUMVUDUXOUWGUGCD
      EFIJUWEUWGLMNPXHVQXGWCXIXJXAUVSUXIUWMUYHXEUUEUYOUWLUYGUWIUMUVQUVRUXIUWLUY
      GUTZUVBXKVHUVHXKVHUWKXKVHVUEUVQUVRUXIUVBUVHUWKXLUVBXMUVHXMUXIUWKVUBXNXOXI
      VMXBXPXQXRXSUVCUVFUVIUVLXTYAWEYBUVPUVGSUPUQZUVMTUPUQZUOUVOAVUFBVUGAUUOVUF
      QUUNUVGGSUPYKUVBUTZYLUVCUUMUVFYKUVBUCUEVKVUHUUKUVEHUULVUHUUJUVDUUHUMYKUVB
      UUIULVLVMYCWCYDYEBUVAVUGRUUTUVMGTUPYKUVHUTZYLUVIUUSUVLYKUVHUCUEVKVUIUURUV
      KHUULVUIUUJUVJUUQUMYKUVHUUIULVLVMYCWCYDYEYFUVGUVMSTUPUPYGYHYAYIYJ $.
      $( [3-Jun-2006] $) $( [1-Jun-2005] $)
  $}

$(

