$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Cauchy sequences and limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w f F $.
    $( Member of the set of Cauchy sequences on a Hilbert space.  Definition
       for Cauchy sequence in [Beran] p. 96. $)
    hcau $p |- ( F e. Cauchy <-> ( F : NN --> ~H /\
          A. x e. RR ( 0 < x ->
          E. y e. NN A. z e. NN A. w e. NN ( ( y <_ z /\ y <_ w ) ->
             ( normh ` ( ( F ` z ) -h ( F ` w ) ) ) < x ) ) ) ) $=
      ( vf ccau wcel cvv cn chil cv clt wbr wa cfv cmv cno wi wral cc0 cle wrex
      wf co elex nnex fex mpan2 adantr wceq fveq1 opreq12d fveq2d breq1d imbi2d
      cr feq1 2ralbidv rexbidv ralbidv anbi12d df-hcau elab2g pm5.21nii ) EGHEI
      HZJKEUDZUAALZMNZBLZCLZUBNVJDLZUBNOZVKEPZVLEPZQUEZRPZVHMNZSZDJTCJTZBJUCZSZ
      AUQTZOZEGUFVGVFWCVGJIHVFUGJKIEUHUIUJJKFLZUDZVIVMVKWEPZVLWEPZQUEZRPZVHMNZS
      ZDJTCJTZBJUCZSZAUQTZOWDFEGIWEEUKZWFVGWPWCJKWEEURWQWOWBAUQWQWNWAVIWQWMVTBJ
      WQWLVSCDJJWQWKVRVMWQWJVQVHMWQWIVPRWQWGVNWHVOQVKWEEULVLWEEULUMUNUOUPUSUTUP
      VAVBABCDFVCVDVE $.
      $( [16-Aug-1999] $)

    $( A Cauchy sequences on a Hilbert space is a sequence. $)
    hcauseq $p |- ( F e. Cauchy -> F : NN --> ~H ) $=
      ( vx vy vz vw ccau wcel cn chil wf cc0 cv clt wbr cle wa cfv cmv wi wral
      co cno wrex cr hcau simplbi ) AFGHIAJKBLZMNCLZDLZONUHELZONPUIAQUJAQRUAUBQ
      UGMNSEHTDHTCHUCSBUDTBCDEAUEUF $.
      $( [16-Aug-1999] $)

    $( A Cauchy sequence on a Hilbert space converges. $)
    hcaucvg $p |- ( F e. Cauchy -> A. x e. RR ( 0 < x ->
          E. y e. NN A. z e. NN A. w e. NN ( ( y <_ z /\ y <_ w ) ->
             ( normh ` ( ( F ` z ) -h ( F ` w ) ) ) < x ) ) ) $=
      ( ccau wcel cn chil wf cc0 cv clt wbr cle wa cfv cmv wi wral co wrex hcau
      cno cr simprbi ) EFGHIEJKALZMNBLZCLZONUHDLZONPUIEQUJEQRUAUDQUGMNSDHTCHTBH
      UBSAUETABCDEUCUF $.
      $( [16-Aug-1999] $)

    $( A sequence on a Hilbert space is a Cauchy sequence if it converges. $)
    seq1hcau $p |- ( F : NN --> ~H -> ( F e. Cauchy <->
          A. x e. RR ( 0 < x ->
          E. y e. NN A. z e. NN A. w e. NN ( ( y <_ z /\ y <_ w ) ->
             ( normh ` ( ( F ` z ) -h ( F ` w ) ) ) < x ) ) ) ) $=
      ( ccau wcel cn chil wf cc0 cv clt wbr cle wa cfv cmv wi wral co wrex hcau
      cno cr baib ) EFGHIEJKALZMNBLZCLZONUHDLZONPUIEQUJEQRUAUDQUGMNSDHTCHTBHUBS
      AUETABCDEUCUF $.
      $( [16-Aug-1999] $)
  $}

  ${
    $d j k m n y x F $.
    $( Alternate representation of a Hilbert space Cauchy sequence. $)
    hcau2 $p |- ( F e. Cauchy <-> ( F : NN --> ~H /\ A. x e. RR
        ( 0 < x -> E. j e. NN A. k e. NN
        ( j <_ k -> ( normh ` ( ( F ` k ) -h ( F ` j ) ) ) < x ) ) ) ) $=
      ( vm wcel cn chil cv clt wbr wa cfv cmv co cno wi wral cr adantr ccau cc0
      vy wf wrex hcau wceq ffvelrn normsub syl2an anandis anassrs breq1d imbi2d
      cle ralbidva rexbidva ralbidv c2 cdiv fveq2 opreq2d opreq1d breq2 anbi12d
      fveq2d w3a 3anim123i 3comr 3anandis hvsubcl ancoms normcl 3adant2 3adant1
      caddc syl simpr lt2halves syl3anc norm3dif2 3adant3 readdcl syl2anc mpand
      lelttr syld sylan an32s nnssz cau3i bitr3d pm5.32i bitr4i ) DUAFGHDUDZUBA
      IZJKZEIZBIZUOKWRCIZUOKLWSDMZWTDMZNOZPMZWPJKZQCGRBGREGUEQASRZLWOWQWSWTUOKZ
      XBXANOPMZWPJKZQZCGRZBGUEZQZASRZLAEBCDUFWOXNXFWOWQXGXEQZCGRZBGUEZQZASRXNXF
      WOXRXMASWOXQXLWQWOXPXKBGWOWSGFZLZXOXJCGXTWTGFZLZXEXIXGYBXDXHWPJWOXSYAXDXH
      UGZWOXSYAYCXTXAHFZXBHFZYCWOYALZGHWSDUHZGHWTDUHZXAXBUIUJUKULUMUNUPUQUNURXE
      XAWRDMZNOZPMZWPJKZXBYINOZPMZWPJKXDUCIZUSUTOZJKZYKYPJKZLZYNYOJKZWOAUCBCEGW
      TWRUGZXDYKWPJUUAXCYJPUUAXBYIXANWTWRDVAVBVFUMWSWTUGZYKYNWPJUUBYJYMPUUBXAXB
      YINWSWTDVAVCVFUMWPYPUGXEYQYLYRWPYPXDJVDWPYPYKJVDVEWPYOYNJVDWOXSYAWRGFZVGZ
      YOSFZYSYTQZWOUUDLYEYIHFZYDVGZUUEUUFWOXSYAUUCUUHYFWOUUCLZXTUUHYFYEUUIUUGXT
      YDYHGHWRDUHYGVHVIVJUUHUUELZYSXDYKVPOZYOJKZYTUUJXDSFZYKSFZUUEYSUULQUUHUUMU
      UEYEYDUUMUUGYEYDLXCHFZUUMYDYEUUOXAXBVKVLXCVMVQVNZTUUHUUNUUEUUGYDUUNYEUUGY
      DLYJHFZUUNYDUUGUUQXAYIVKVLYJVMVQVOZTUUHUUEVRZXDYKYOVSVTUUJYNUUKUOKZUULYTU
      UHUUTUUEXBYIXAWATUUJYNSFZUUKSFZUUEUUTUULLYTQUUHUVAUUEYEUUGUVAYDYEUUGLYMHF
      UVAXBYIVKYMVMVQWBTUUHUVBUUEUUHUUMUUNUVBUUPUURXDYKWCWDTUUSYNUUKYOWFVTWEWGW
      HWIWJWKWLWMWN $.
      $( [17-Apr-2007] $)
  $}

  ${
    $d x y z w f F $.  $d x y z w f A $.
    hlim.1 $e |- F e. _V $.
    hlim.2 $e |- A e. _V $.
    $( Express the predicate:  The limit of vector sequence ` F ` in a Hilbert
       space is ` A ` , i.e. ` F ` converges to ` A ` .  This means that for
       any real ` x ` , no matter how small, there always exists an integer
       ` y ` such that the norm of any later vector in the sequence minus the
       limit is less than ` x ` .  Definition of converge in [Beran] p. 96. $)
    hlimi $p |- ( F ~~>v A <-> ( ( F : NN --> ~H /\ A e. ~H ) /\
          A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( normh ` ( ( F ` z ) -h A ) ) < x ) ) ) ) $=
      ( cn chil cv wa clt wbr cfv cmv cno wi wral cr imbi2d vf vw wf cc0 cle co
      wcel wrex chli wceq anbi1d fveq1 opreq1d fveq2d breq1d rexralbidv ralbidv
      feq1 anbi12d eleq1 anbi2d opreq2 df-hlim brab ) HIUAJZUCZUBJZIUGZKZUDAJZL
      MZBJCJZUEMZVLVENZVGOUFZPNZVJLMZQZCHRBHUHZQZASRZKHIEUCZVHKZVKVMVLENZVGOUFZ
      PNZVJLMZQZCHRBHUHZQZASRZKWBDIUGZKZVKVMWDDOUFZPNZVJLMZQZCHRBHUHZQZASRZKUAU
      BEDUIFGVEEUJZVIWCWAWKXAVFWBVHHIVEEURUKXAVTWJASXAVSWIVKXAVRWHBCHHXAVQWGVMX
      AVPWFVJLXAVOWEPXAVNWDVGOVLVEEULUMUNUOTUPTUQUSVGDUJZWCWMWKWTXBVHWLWBVGDIUT
      VAXBWJWSASXBWIWRVKXBWHWQBCHHXBWGWPVMXBWFWOVJLXBWEWNPVGDWDOVBUNUOTUPTUQUSA
      BCUBUAVCVD $.
      $( [16-Aug-1999] $)

    $( A sequence with a limit on a Hilbert space is a sequence. $)
    hlimseqi $p |- ( F ~~>v A -> F : NN --> ~H ) $=
      ( vx vy vz chli wbr cn chil wf wcel wa cc0 cv clt cfv wi wral cle cmv cno
      co wrex cr hlimi simplbi simpld ) BAHIZJKBLZAKMZUJUKULNOEPZQIFPGPZUAIUNBR
      AUBUDUCRUMQISGJTFJUESEUFTEFGABCDUGUHUI $.
      $( [16-Aug-1999] $)

    $( Closure of the limit of a sequence on Hilbert space. $)
    hlimveci $p |- ( F ~~>v A -> A e. ~H ) $=
      ( vx vy vz chli wbr cn chil wf wcel wa cc0 cv clt cfv wi wral cle cmv cno
      co wrex cr hlimi simplbi simprd ) BAHIZJKBLZAKMZUJUKULNOEPZQIFPGPZUAIUNBR
      AUBUDUCRUMQISGJTFJUESEUFTEFGABCDUGUHUI $.
      $( [16-Aug-1999] $)

    $( Convergence of a sequence on a Hilbert space. $)
    hlimconvi $p |- ( F ~~>v A -> A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( normh ` ( ( F ` z ) -h A ) ) < x ) ) ) $=
      ( chli wbr cn chil wf wcel wa cc0 cv clt cfv wi wral cle cmv co cno hlimi
      wrex cr simprbi ) EDHIJKELDKMNOAPZQIBPCPZUAIUJERDUBUCUDRUIQISCJTBJUFSAUGT
      ABCDEFGUEUH $.
      $( [16-Aug-1999] $)
  $}

  ${
    $d x y z w f F $.  $d x y z w f A $.
    $( The limit of a sequence on a Hilbert space. $)
    hlim2 $p |- ( ( F : NN --> ~H /\ A e. ~H ) -> ( F ~~>v A <->
          A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( normh ` ( ( F ` z ) -h A ) ) < x ) ) ) ) $=
      ( cn chil wcel wa wbr cv clt cfv cmv cno wi wral cr cvv imbi2d vf vw chli
      wf cc0 cle co wrex wb nnex fex mpan2 wceq feq1 anbi1d fveq1 fveq2d breq1d
      opreq1d ralbidv anbi12d eleq1 anbi2d opreq2 df-hlim brabg sylan bianabs
      rexralbidv ) FGEUDZDGHZIZEDUCJZUEAKZLJZBKCKZUFJZVPEMZDNUGZOMZVNLJZPZCFQBF
      UHZPZARQZVJESHZVKVMVLWEIZUIVJFSHWFUJFGSEUKULFGUAKZUDZUBKZGHZIZVOVQVPWHMZW
      JNUGZOMZVNLJZPZCFQBFUHZPZARQZIVJWKIZVOVQVRWJNUGZOMZVNLJZPZCFQBFUHZPZARQZI
      WGUAUBEDSGUCWHEUMZWLXAWTXHXIWIVJWKFGWHEUNUOXIWSXGARXIWRXFVOXIWQXEBCFFXIWP
      XDVQXIWOXCVNLXIWNXBOXIWMVRWJNVPWHEUPUSUQURTVITUTVAWJDUMZXAVLXHWEXJWKVKVJW
      JDGVBVCXJXGWDARXJXFWCVOXJXEWBBCFFXJXDWAVQXJXCVTVNLXJXBVSOWJDVRNVDUQURTVIT
      UTVAABCUBUAVEVFVGVH $.
      $( [16-Aug-1999] $)
  $}

