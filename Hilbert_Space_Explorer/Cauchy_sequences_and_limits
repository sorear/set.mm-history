$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Cauchy sequences and limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z f F $.  $d x y z A $.
    $( Member of the set of Cauchy sequences on a Hilbert space.  Definition
       for Cauchy sequence in [Beran] p. 96.  (Revised by Mario Carneiro,
       14-May-2014.) $)
    hcau $p |- ( F e. Cauchy <-> ( F : NN --> ~H /\
          A. x e. RR+ E. y e. NN A. z e. ( ZZ>= ` y )
             ( normh ` ( ( F ` y ) -h ( F ` z ) ) ) < x ) ) $=
      ( vf ccau wcel chil cn co cv cfv cmv cno clt wbr wral wrex crp wa cmap wf
      cuz wceq oveq12d fveq2d breq1d rexralbidv ralbidv df-hcau elrab2 ax-hilex
      fveq1 nnex elmap anbi1i bitri ) DFGDHIUAJZGZBKZDLZCKZDLZMJZNLZAKZOPZCUTUC
      LZQBIRZASQZTIHDUBZVJTUTEKZLZVBVLLZMJZNLZVFOPZCVHQBIRZASQVJEDURFVLDUDZVRVI
      ASVSVQVGBCIVHVSVPVEVFOVSVOVDNVSVMVAVNVCMUTVLDUMVBVLDUMUEUFUGUHUIABCEUJUKU
      SVKVJHIDULUNUOUPUQ $.
      $( [14-May-2014] $) $( [16-Aug-1999] $)

    $( A Cauchy sequences on a Hilbert space is a sequence. $)
    hcauseq $p |- ( F e. Cauchy -> F : NN --> ~H ) $=
      ( vy vz vx ccau wcel cn chil wf cv cfv cmv cno clt wbr cuz wral wrex crp
      co hcau simplbi ) AEFGHAIBJZAKCJAKLTMKDJNOCUCPKQBGRDSQDBCAUAUB $.
      $( [14-May-2014] $) $( [16-Aug-1999] $)

    $( A Cauchy sequence on a Hilbert space converges.  (Revised by Mario
       Carneiro, 14-May-2014.) $)
    hcaucvg $p |- ( ( F e. Cauchy /\ A e. RR+ ) -> E. y e. NN
           A. z e. ( ZZ>= ` y ) ( normh ` ( ( F ` y ) -h ( F ` z ) ) ) < A ) $=
      ( vx ccau wcel cv cfv cmv co cno clt wbr cuz wral cn wrex crp chil breq2
      wf hcau simprbi wceq rexralbidv rcla4cva sylan ) DFGZAHZDIBHDIJKLIZEHZMNZ
      BUJOIZPAQRZESPZCSGUKCMNZBUNPAQRZUIQTDUBUPEABDUCUDUOURECSULCUEUMUQABQUNULC
      UKMUAUFUGUH $.
      $( [14-May-2014] $) $( [16-Aug-1999] $)

    $( A sequence on a Hilbert space is a Cauchy sequence if it converges.
       (Revised by Mario Carneiro, 14-May-2014.) $)
    seq1hcau $p |- ( F : NN --> ~H -> ( F e. Cauchy <->
          A. x e. RR+ E. y e. NN A. z e. ( ZZ>= ` y )
             ( normh ` ( ( F ` y ) -h ( F ` z ) ) ) < x ) ) $=
      ( ccau wcel cn chil wf cv cfv cmv co cno clt wbr cuz wral wrex crp hcau
      baib ) DEFGHDIBJZDKCJDKLMNKAJOPCUCQKRBGSATRABCDUAUB $.
      $( [14-May-2014] $) $( [16-Aug-1999] $)
  $}

  ${
    $d x y z w f F $.  $d x y z w f A $.  $d x y z B $.
    hlim.1 $e |- A e. _V $.
    $( Express the predicate:  The limit of vector sequence ` F ` in a Hilbert
       space is ` A ` , i.e. ` F ` converges to ` A ` .  This means that for
       any real ` x ` , no matter how small, there always exists an integer
       ` y ` such that the norm of any later vector in the sequence minus the
       limit is less than ` x ` .  Definition of converge in [Beran] p. 96.
       (Revised by Mario Carneiro, 14-May-2014.) $)
    hlimi $p |- ( F ~~>v A <-> ( ( F : NN --> ~H /\ A e. ~H ) /\
          A. x e. RR+ E. y e. NN A. z e. ( ZZ>= ` y )
                ( normh ` ( ( F ` z ) -h A ) ) < x ) ) $=
      ( vf vw chli wbr cvv wcel cn chil wa cv cfv cmv wral wceq wf cno clt wrex
      co cuz crp df-hlim relopabi brrelexi nnex mpan2 ad2antrr wb feq1 bi2anan9
      fex eleq1 fveq1 oveq12 fveq2d breq1d rexralbidv ralbidv anbi12d pm5.21nii
      sylan brabga ) EDIJZEKLZMNEUAZDNLZOZCPZEQZDRUEZUBQZAPZUCJZCBPUFQZSBMUDZAU
      GSZOZEDIMNGPZUAZHPZNLZOZVNWDQZWFRUEZUBQZVRUCJZCVTSBMUDZAUGSZOZGHIABCHGUHZ
      UIUJVKVJVLWBVKMKLVJUKMNKEUQULUMVJDKLVIWCUNFWOWCGHEDIKKWDETZWFDTZOZWHVMWNW
      BWQWEVKWRWGVLMNWDEUOWFDNURUPWSWMWAAUGWSWLVSBCMVTWSWKVQVRUCWSWJVPUBWQWIVOT
      WRWJVPTVNWDEUSWIVOWFDRUTVGVAVBVCVDVEWPVHULVF $.
      $( [14-May-2014] $) $( [16-Aug-1999] $)

    $( A sequence with a limit on a Hilbert space is a sequence. $)
    hlimseqi $p |- ( F ~~>v A -> F : NN --> ~H ) $=
      ( vz vx vy chli wbr cn chil wf wcel wa cv cfv cmv co cno clt wral cuz crp
      wrex hlimi simplbi simpld ) BAGHZIJBKZAJLZUGUHUIMDNBOAPQROENSHDFNUAOTFIUC
      EUBTEFDABCUDUEUF $.
      $( [16-Aug-1999] $)

    $( Closure of the limit of a sequence on Hilbert space. $)
    hlimveci $p |- ( F ~~>v A -> A e. ~H ) $=
      ( vz vx vy chli wbr cn chil wf wcel wa cv cfv cmv co cno clt wral cuz crp
      wrex hlimi simplbi simprd ) BAGHZIJBKZAJLZUGUHUIMDNBOAPQROENSHDFNUAOTFIUC
      EUBTEFDABCUDUEUF $.
      $( [16-Aug-1999] $)

    $( Convergence of a sequence on a Hilbert space.  (Revised by Mario
       Carneiro, 14-May-2014.) $)
    hlimconvi $p |- ( ( F ~~>v A /\ B e. RR+ ) ->
      E. y e. NN A. z e. ( ZZ>= ` y ) ( normh ` ( ( F ` z ) -h A ) ) < B ) $=
      ( vx chli wbr cv cfv cmv co clt wral cn wrex crp wcel chil cno wf simprbi
      cuz wa hlimi wceq breq2 rexralbidv rcla4cva sylan ) ECHIZBJEKCLMUAKZGJZNI
      ZBAJUDKZOAPQZGROZDRSUMDNIZBUPOAPQZULPTEUBCTSUEURGABCEFUFUCUQUTGDRUNDUGUOU
      SABPUPUNDUMNUHUIUJUK $.
      $( [14-May-2014] $) $( [16-Aug-1999] $)
  $}

  ${
    $d x y z w f F $.  $d x y z w f A $.
    $( The limit of a sequence on a Hilbert space. $)
    hlim2 $p |- ( ( F : NN --> ~H /\ A e. ~H ) -> ( F ~~>v A <->
          A. x e. RR+ E. y e. NN A. z e. ( ZZ>= ` y )
                ( normh ` ( ( F ` z ) -h A ) ) < x ) ) $=
      ( vw chil wcel cn chli wbr cv cfv cmv co cno clt wral wrex crp wf wb wceq
      cuz wi breq2 oveq2 fveq2d breq1d rexralbidv ralbidv bibi12d imbi2d wa vex
      hlimi baib expcom vtoclga impcom ) DGHIGEUAZEDJKZCLEMZDNOZPMZALZQKZCBLUDM
      ZRBISZATRZUBZVAEFLZJKZVCVLNOZPMZVFQKZCVHRBISZATRZUBZUEVAVKUEFDGVLDUCZVSVK
      VAVTVMVBVRVJVLDEJUFVTVQVIATVTVPVGBCIVHVTVOVEVFQVTVNVDPVLDVCNUGUHUIUJUKULU
      MVAVLGHZVSVMVAWAUNVRABCVLEFUOUPUQURUSUT $.
      $( [14-May-2014] $) $( [16-Aug-1999] $)
  $}

  ${
    $d n F $.  $d n G $.  $d n ph $.
    hlimadd.1 $e |- ( ph -> A e. V ) $.
    hlimadd.2 $e |- ( ph -> B e. W ) $.
    hlimadd.3 $e |- ( ph -> F : NN --> ~H ) $.
    hlimadd.4 $e |- ( ph -> G : NN --> ~H ) $.
    hlimadd.5 $e |- ( ph -> F ~~>v A ) $.
    hlimadd.6 $e |- ( ph -> G ~~>v B ) $.
    hlimadd.7 $e |- H = ( n e. NN |-> ( ( F ` n ) +h ( G ` n ) ) ) $.
    $( Limit of the sum of two sequences in a Hilbert vector space.
       (Contributed by Mario Carneiro, 19-May-2014.) $)
    hlimadd $p |- ( ph -> H ~~>v ( A +h B ) ) $=
      ( cva chil cn wcel co cno cmv ccom copn cfv clm wbr cmap chli c1 nnuz cme
      cuni wceq csm cop eqid hhims hhmet hhmetba uniopn2 ax-mp cz 1z a1i opntop
      ctop cres hhnv df-hba h2hlm resss eqsstri ssbri syl ctx ccn cnv hhva vacn
      lmcn2 wf cv wa ffvelrn sylan hvaddcl syl2anc ex fmptd ax-hilex nnex elmap
      sylibr breqi ovex brres bitri sylanbrc ) AGBCQUAZUBUCUDZUEUFZUGUFZUHZGRSU
      IUAZTZGXAUJUHZABCDEFGXCXCUKXCQHIRRSULXBUMTZRXCUNUOXBQUPUQUBUQZXJURZXBXJXK
      XBURUSZUTZXBXCRXBXJXKXLVAXCURZVBVCZXOUKVDTAVEVFXCVHTZAXIXPXMXBXCXNVGVCVFZ
      XQJKLMXQAEBUJUHEBXDUHNUJXDEBUJXDXFVIZXDXBXJXCXKXJXKVJZVKXLXNVLZXDXFVMVNZV
      OVPAFCUJUHFCXDUHOUJXDFCYAVOVPQXCXCVQUAXCVRUATZAXJVSTYBXSXBXJQXCXLXNXJXKVT
      WAVCVFPWBASRGWCXGADSDWDZEUFZYCFUFZQUAZRGAYCSTZYFRTZAYGWEYDRTZYERTZYHASREW
      CYGYILSRYCEWFWGASRFWCYGYJMSRYCFWFWGYDYEWHWIWJPWKRSGWLWMWNWOXHGXAXRUHXEXGW
      EGXAUJXRXTWPGXAXDXFBCQWQWRWSWT $.
      $( [19-May-2014] $)
  $}

