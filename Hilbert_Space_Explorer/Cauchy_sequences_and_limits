$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Cauchy sequences and limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w f F $.
    $( Member of the set of Cauchy sequences on a Hilbert space.  Definition
       for Cauchy sequence in [Beran] p. 96. $)
    hcau $p |- ( F e. Cauchy <-> ( F : NN --> ~H /\
          A. x e. RR ( 0 < x ->
          E. y e. NN A. z e. NN A. w e. NN ( ( y <_ z /\ y <_ w ) ->
             ( normh ` ( ( F ` z ) -h ( F ` w ) ) ) < x ) ) ) ) $=
      ( vf ccau wcel cvv cn chil wf cc0 cv clt wbr cle wa cfv cmv co cno wi
      wral wrex cr elisset nnex fex mpan2 adantr wceq feq1 fveq1 opreq12d
      fveq2d breq1d imbi2d 2ralbidv rexbidv ralbidv anbi12d df-hcau elab2g
      pm5.21nii ) EGHEIHZJKELZMANZOPZBNZCNZQPVJDNZQPRZVKESZVLESZTUAZUBSZVHOPZUC
      ZDJUDCJUDZBJUEZUCZAUFUDZRZEGUGVGVFWCVGJIHVFUHJKIEUIUJUKJKFNZLZVIVMVKWESZV
      LWESZTUAZUBSZVHOPZUCZDJUDCJUDZBJUEZUCZAUFUDZRWDFEGIWEEULZWFVGWPWCJKWEEUMW
      QWOWBAUFWQWNWAVIWQWMVTBJWQWLVSCDJJWQWKVRVMWQWJVQVHOWQWIVPUBWQWGVNWHVOTVKW
      EEUNVLWEEUNUOUPUQURUSUTURVAVBABCDFVCVDVE $.
      $( [16-Aug-1999] $)

    $( A Cauchy sequences on a Hilbert space is a sequence. $)
    hcauseq $p |- ( F e. Cauchy -> F : NN --> ~H ) $=
      ( vx vy vz vw ccau wcel cn chil wf cc0 cv clt wbr cle wa cfv cmv co cno
      wi wral wrex cr hcau simplbi ) AFGHIAJKBLZMNCLZDLZONUHELZONPUIAQUJAQRSTQU
      GMNUAEHUBDHUBCHUCUABUDUBBCDEAUEUF $.
      $( [16-Aug-1999] $)

    $( A Cauchy sequence on a Hilbert space converges. $)
    hcaucvg $p |- ( F e. Cauchy -> A. x e. RR ( 0 < x ->
          E. y e. NN A. z e. NN A. w e. NN ( ( y <_ z /\ y <_ w ) ->
             ( normh ` ( ( F ` z ) -h ( F ` w ) ) ) < x ) ) ) $=
      ( ccau wcel cn chil wf cc0 cv clt wbr cle wa cfv cmv co cno wi wral wrex
      cr hcau simprbi ) EFGHIEJKALZMNBLZCLZONUHDLZONPUIEQUJEQRSTQUGMNUADHUBCHUB
      BHUCUAAUDUBABCDEUEUF $.
      $( [16-Aug-1999] $)

    $( A sequence on a Hilbert space is a Cauchy sequence if it converges. $)
    seq1hcau $p |- ( F : NN --> ~H -> ( F e. Cauchy <->
          A. x e. RR ( 0 < x ->
          E. y e. NN A. z e. NN A. w e. NN ( ( y <_ z /\ y <_ w ) ->
             ( normh ` ( ( F ` z ) -h ( F ` w ) ) ) < x ) ) ) ) $=
      ( ccau wcel cn chil wf cc0 cv clt wbr cle wa cfv cmv co cno wi wral wrex
      cr hcau baib ) EFGHIEJKALZMNBLZCLZONUHDLZONPUIEQUJEQRSTQUGMNUADHUBCHUBBHU
      CUAAUDUBABCDEUEUF $.
      $( [16-Aug-1999] $)
  $}

  ${
    $d j k m n y x F $.
    $( Alternate representation of a Hilbert space Cauchy sequence. $)
    hcau2 $p |- ( F e. Cauchy <-> ( F : NN --> ~H /\ A. x e. RR
        ( 0 < x -> E. j e. NN A. k e. NN
        ( j <_ k -> ( normh ` ( ( F ` k ) -h ( F ` j ) ) ) < x ) ) ) ) $=
      ( vm vy ccau wcel cn chil wf cc0 cv clt wbr cle wa cfv cmv co cno wi
      wral wrex cr hcau wceq normsub ffvelrn syl2an anandis anassrs breq1d
      imbi2d ralbidva rexbidva ralbidv c2 cdiv fveq2 opreq2d fveq2d opreq1d
      breq2 anbi12d w3a caddc hvsubcl ancoms normcl syl 3adant2 adantr 3adant1
      simpr lt2halves syl111anc norm3dif2 3adant3 readdcl syl11anc lelttr
      mpand syld 3anim123i 3comr 3anandis sylan an32s nnssz cau3i bitr3d
      pm5.32i bitr4i ) DGHIJDKZLAMZNOZEMZBMZPOWRCMZPOQWSDRZWTDRZSTZUARZWPNOZUBC
      IUCBIUCEIUDUBAUEUCZQWOWQWSWTPOZXBXASTUARZWPNOZUBZCIUCZBIUDZUBZAUEUCZQAEBC
      DUFWOXNXFWOWQXGXEUBZCIUCZBIUDZUBZAUEUCXNXFWOXRXMAUEWOXQXLWQWOXPXKBIWOWSIH
      ZQZXOXJCIXTWTIHZQZXEXIXGYBXDXHWPNWOXSYAXDXHUGZWOXSYAYCXAJHZXBJHZYCXTWOYAQ
      ZXAXBUHIJWSDUIZIJWTDUIZUJUKULUMUNUOUPUNUQXEXAWRDRZSTZUARZWPNOZXBYISTZUARZ
      WPNOXDFMZURUSTZNOZYKYPNOZQZYNYONOZWOAFBCEIWTWRUGZXDYKWPNUUAXCYJUAUUAXBYIX
      ASWTWRDUTVAVBUMWSWTUGZYKYNWPNUUBYJYMUAUUBXAXBYISWSWTDUTVCVBUMWPYPUGXEYQYL
      YRWPYPXDNVDWPYPYKNVDVEWPYOYNNVDWOXSYAWRIHZVFZYOUEHZYSYTUBZYEYIJHZYDVFZUUE
      UUFWOUUDQUUHUUEQZYSXDYKVGTZYONOZYTUUIXDUEHZYKUEHZUUEYSUUKUBUUHUULUUEYEYDU
      ULUUGYEYDQXCJHZUULYDYEUUNXAXBVHVIXCVJVKVLZVMUUHUUMUUEUUGYDUUMYEUUGYDQYJJH
      ZUUMYDUUGUUPXAYIVHVIYJVJVKVNZVMUUHUUEVOZXDYKYOVPVQUUIYNUUJPOZUUKYTUUHUUSU
      UEXBYIXAVRVMUUIYNUEHZUUJUEHZUUEUUSUUKQYTUBUUHUUTUUEYEUUGUUTYDYEUUGQYMJHUU
      TXBYIVHYMVJVKVSVMUUHUVAUUEUUHUULUUMUVAUUOUUQXDYKVTWAVMUURYNUUJYOWBVQWCWDW
      OXSYAUUCUUHYFWOUUCQZXTUUHYFYEUVBUUGXTYDYHIJWRDUIYGWEWFWGWHWIWJWKWLWMWN $.
      $( [17-Apr-2007] $)
  $}

  ${
    $d x y z w f F $.  $d x y z w f A $.
    hlim.1 $e |- F e. _V $.
    hlim.2 $e |- A e. _V $.
    $( Express the predicate:  The limit of vector sequence ` F ` in a Hilbert
       space is ` A ` , i.e. ` F ` converges to ` A ` .  This means that for
       any real ` x ` , no matter how small, there always exists an integer
       ` y ` such that the norm of any later vector in the sequence minus the
       limit is less than ` x ` .  Definition of converge in [Beran] p. 96. $)
    hlimi $p |- ( F ~~>v A <-> ( ( F : NN --> ~H /\ A e. ~H ) /\
          A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( normh ` ( ( F ` z ) -h A ) ) < x ) ) ) ) $=
      ( vf vw cn chil cv wf wcel wa cc0 clt wbr cle cfv cmv co cno wi wral
      wrex cr chli wceq feq1 anbi1d fveq1 opreq1d fveq2d breq1d imbi2d
      rexralbidv ralbidv anbi12d eleq1 anbi2d opreq2 df-hlim brab ) JKHLZMZILZK
      NZOZPALZQRZBLCLZSRZVLVETZVGUAUBZUCTZVJQRZUDZCJUEBJUFZUDZAUGUEZOJKEMZVHOZV
      KVMVLETZVGUAUBZUCTZVJQRZUDZCJUEBJUFZUDZAUGUEZOWBDKNZOZVKVMWDDUAUBZUCTZVJQ
      RZUDZCJUEBJUFZUDZAUGUEZOHIEDUHFGVEEUIZVIWCWAWKXAVFWBVHJKVEEUJUKXAVTWJAUGX
      AVSWIVKXAVRWHBCJJXAVQWGVMXAVPWFVJQXAVOWEUCXAVNWDVGUAVLVEEULUMUNUOUPUQUPUR
      USVGDUIZWCWMWKWTXBVHWLWBVGDKUTVAXBWJWSAUGXBWIWRVKXBWHWQBCJJXBWGWPVMXBWFWO
      VJQXBWEWNUCVGDWDUAVBUNUOUPUQUPURUSABCIHVCVD $.
      $( [16-Aug-1999] $)

    $( A sequence with a limit on a Hilbert space is a sequence. $)
    hlimseqi $p |- ( F ~~>v A -> F : NN --> ~H ) $=
      ( vx vy vz chli wbr cn chil wf wcel wa cc0 cv clt cle cfv cmv co cno wi
      wral wrex cr hlimi simplbi simpld ) BAHIZJKBLZAKMZUJUKULNOEPZQIFPGPZRIUNB
      SATUAUBSUMQIUCGJUDFJUEUCEUFUDEFGABCDUGUHUI $.
      $( [16-Aug-1999] $)

    $( Closure of the limit of a sequence on Hilbert space. $)
    hlimveci $p |- ( F ~~>v A -> A e. ~H ) $=
      ( vx vy vz chli wbr cn chil wf wcel wa cc0 cv clt cle cfv cmv co cno wi
      wral wrex cr hlimi simplbi simprd ) BAHIZJKBLZAKMZUJUKULNOEPZQIFPGPZRIUNB
      SATUAUBSUMQIUCGJUDFJUEUCEUFUDEFGABCDUGUHUI $.
      $( [16-Aug-1999] $)

    $( Convergence of a sequence on a Hilbert space. $)
    hlimconvi $p |- ( F ~~>v A -> A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( normh ` ( ( F ` z ) -h A ) ) < x ) ) ) $=
      ( chli wbr cn chil wf wcel wa cc0 cv clt cle cfv cmv co cno wi wral wrex
      cr hlimi simprbi ) EDHIJKELDKMNOAPZQIBPCPZRIUJESDTUAUBSUIQIUCCJUDBJUEUCAU
      FUDABCDEFGUGUH $.
      $( [16-Aug-1999] $)
  $}

  ${
    $d x y z w f F $.  $d x y z w f A $.
    $( The limit of a sequence on a Hilbert space. $)
    hlim2 $p |- ( ( F : NN --> ~H /\ A e. ~H ) -> ( F ~~>v A <->
          A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( normh ` ( ( F ` z ) -h A ) ) < x ) ) ) ) $=
      ( vf vw cn chil wf wcel wa chli wbr cc0 cv clt cle cfv cmv co cno wi
      wral wrex cr cvv wb wceq feq1 anbi1d fveq1 opreq1d fveq2d breq1d imbi2d
      rexralbidv ralbidv anbi12d eleq1 anbi2d opreq2 df-hlim brabg nnex fex
      mpan2 sylan bianabs ) HIEJZDIKZLZEDMNZOAPZQNZBPCPZRNZVPESZDTUAZUBSZVNQNZU
      CZCHUDBHUEZUCZAUFUDZEUGKZVKVMVLWELZUHVJHIFPZJZGPZIKZLZVOVQVPWHSZWJTUAZUBS
      ZVNQNZUCZCHUDBHUEZUCZAUFUDZLVJWKLZVOVQVRWJTUAZUBSZVNQNZUCZCHUDBHUEZUCZAUF
      UDZLWGFGEDUGIMWHEUIZWLXAWTXHXIWIVJWKHIWHEUJUKXIWSXGAUFXIWRXFVOXIWQXEBCHHX
      IWPXDVQXIWOXCVNQXIWNXBUBXIWMVRWJTVPWHEULUMUNUOUPUQUPURUSWJDUIZXAVLXHWEXJW
      KVKVJWJDIUTVAXJXGWDAUFXJXFWCVOXJXEWBBCHHXJXDWAVQXJXCVTVNQXJXBVSUBWJDVRTVB
      UNUOUPUQUPURUSABCGFVCVDVJHUGKWFVEHIUGEVFVGVHVI $.
      $( [16-Aug-1999] $)
  $}

