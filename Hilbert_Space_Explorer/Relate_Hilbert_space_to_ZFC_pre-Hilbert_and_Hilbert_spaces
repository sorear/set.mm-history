$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Relate Hilbert space to ZFC pre-Hilbert and Hilbert spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f j k t u v w x y z D $.
    hhcms.1 $e |- U = <. <. +h , .h >. , normh >. $.
    hhcms.2 $e |- D = ( IndMet ` U ) $.
    $( The Hilbert space induced metric determines a complete metric space. $)
    hhcms $p |- D e. CMet $=
      ( vx vf vy vj vk chil hhmetba hhmet cv cca cfv wcel cn wf wa chli wbr
      wrex clm simpr cc0 clt cle cmv co cno wi wral cr cme wb c1 1z nnuz
      iscauf mpan ffvelrn adantr adantlr hhmetdval normsub eqtrd breq1d
      syl11anc imbi2d ralbidva rexbidva ralbidv bitrd biimpac ccau hcau2
      ax-hcompl sylbir lmbrf2 mp3an1 ancoms visset hlimconvi wceq hlimseqi
      sylan hlimveci rexbidv mpbird syl5bir reximdva sylc iscms2i ) EAFJABCDKZA
      BCDLZFMZANOPZQJWPRZSZWRWPEMZTUAZEJUBZWPWTAUCOUAZEJUBWQWRUDZWSWRUEGMZUFUAZ
      HMZIMZUGUAZXHWPOZXGWPOZUHUIUJOZXEUFUAZUKZIQULZHQUBZUKZGUMULZXBXDWRWQXRWRW
      QXFXIXKXJAUIZXEUFUAZUKZIQULZHQUBZUKZGUMULZXRAUNPZWRWQYEUOWOGAHIWPUPJQWNUQ
      URUSUTWRYDXQGUMWRYCXPXFWRYBXOHQWRXGQPZSZYAXNIQYHXHQPZSZXTXMXIYJXKJPZXJJPZ
      XTXMUOYHYKYIQJXGWPVAVBWRYIYLYGQJXHWPVAZVCYKYLSZXSXLXEUFYNXSXKXJUHUIUJOXLX
      KXJABCDVDXKXJVEVFVGVHVIVJVKVIVLVMVNWRXRSWPVOPXBGHIWPVPEWPVQVRVHWRXAXCEJWR
      WTJPZSXCXFXIXJWTAUIZXEUFUAZUKZIQULZHQUBZUKZGUMULZXAYOWRXCUUBUOZYFYOWRUUCW
      OGAWTHIWPUPJQWNUQURVSVTWAXAUUBXFXIXJWTUHUIUJOZXEUFUAZUKZIQULZHQUBZUKZGUMU
      LGHIWTWPFWBZEWBZWCXAUUAUUIGUMXAYTUUHXFXAYSUUGHQXAYRUUFIQXAYISZYQUUEXIUULY
      PUUDXEUFUULYLYOYPUUDWDWRYIYLXAYMWTWPUUJUUKWEWFXAYOYIWTWPUUJUUKWGVBXJWTABC
      DVDVHVGVIVJWHVIVLWIWJWKWLWM $.
      $( [10-Apr-2008] $)
  $}

  ${
    hhhl.1 $e |- U = <. <. +h , .h >. , normh >. $.
    $( The Hilbert space structure is a complex Hilbert space. $)
    hhhl $p |- U e. CHil $=
      ( chl wcel cbn cphl ishl cnv cims cfv cms eqid isbn hhnv hhcms mpbir2an
      hhph ) ACDAEDZAFDAGRAHDAIJZKDSASLZMABNSABTOPABQP $.
      $( [10-Apr-2008] $)
  $}

  ${
    hilcms.1 $e |- D = ( normh o. -h ) $.
    $( The Hilbert space norm determines a complete metric space. $)
    hilcms $p |- D e. CMet $=
      ( cva csm cop cno eqid hhims hhcms ) ACDEFEZJGZAJKBHI $.
      $( [17-Apr-2007] $)
  $}

  ${
    $d x y z $.
    $( The Hilbert space of the Hilbert Space Explorer is a complex Hilbert
       space.  (Contributed by Steve Rodriguez, 29-Apr-2007.) $)
    hilhl $p |- <. <. +h , .h >. , normh >. e. CHil $=
      ( cva csm cop cno eqid hhhl ) ABCDCZGEF $.
      $( [29-Apr-2007] $)
  $}

