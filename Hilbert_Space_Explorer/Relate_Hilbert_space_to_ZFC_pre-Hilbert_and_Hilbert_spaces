$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Relate Hilbert space to ZFC pre-Hilbert and Hilbert spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f j k t u v w x y z D $.
    hhcms.1 $e |- U = <. <. +h , .h >. , normh >. $.
    hhcms.2 $e |- D = ( IndMet ` U ) $.
    $( The Hilbert space induced metric determines a complete metric space. $)
    hhcms $p |- D e. CMet $=
      ( vx vf vy vj vk chil hhmetba hhmet cn cv wf chli wbr wrex clm cfv cca
      wcel wa cc0 clt cle co wi wral cr wb cme c1 1z nnuz lmbrf2 mp3an1 ancoms
      cmv cno visset hlimconvi wceq hhmetdval ffvelrn hlimseqi sylan hlimveci
      adantr sylanc breq1d imbi2d ralbidva rexbidv ralbidv mpbird syl5bir
      reximdva pm3.27 ccau hcau2 ax-hcompl sylbir iscauf mpan normsub eqtrd
      adantlr rexbidva bitrd biimpac sylc iscms2i ) EAFJABCDKZABCDLZMJFNZOZWPEN
      ZPQZEJRZWPWRASTQZEJRWPAUATUBZWQUCZWQWSXAEJWQWRJUBZUCXAUDGNZUEQZHNZINZUFQZ
      XHWPTZWRAUGZXEUEQZUHZIMUIZHMRZUHZGUJUIZWSXDWQXAXQUKZAULUBZXDWQXRWOGAWRHIW
      PUMJMWNUNUOUPUQURWSXQXFXIXJWRUSUGUTTZXEUEQZUHZIMUIZHMRZUHZGUJUIGHIWRWPFVA
      ZEVAZVBWSXPYEGUJWSXOYDXFWSXNYCHMWSXMYBIMWSXHMUBZUCZXLYAXIYIXKXTXEUEXJJUBZ
      XDXKXTVCYIXJWRABCDVDWQYHYJWSMJXHWPVEZWRWPYFYGVFVGWSXDYHWRWPYFYGVHVIVJVKVL
      VMVNVLVOVPVQVRXBWQVSZWQXFXIXJXGWPTZUSUGUTTZXEUEQZUHZIMUIZHMRZUHZGUJUIZWTX
      CWQYTUCWPVTUBWTGHIWPWAEWPWBWCYLWQXBYTWQXBXFXIYMXJAUGZXEUEQZUHZIMUIZHMRZUH
      ZGUJUIZYTXSWQXBUUGUKWOGAHIWPUMJMWNUNUOWDWEWQUUFYSGUJWQUUEYRXFWQUUDYQHMWQX
      GMUBZUCZUUCYPIMUUIYHUCZUUBYOXIYMJUBZYJUUBYOUKUUJUUKYJUCZUUAYNXEUEUULUUAYM
      XJUSUGUTTYNYMXJABCDVDYMXJWFWGVKUUIUUKYHMJXGWPVEVIWQYHYJUUHYKWHVJVLVMWIVLV
      OWJWKVJWLWM $.
      $( [13-Apr-2008] $) $( [10-Apr-2008] $)
  $}

  ${
    hhhl.1 $e |- U = <. <. +h , .h >. , normh >. $.
    $( The Hilbert space structure is a complex Hilbert space. $)
    hhhl $p |- U e. CHil $=
      ( chl wcel cbn cphl ishl cnv cims cfv cms eqid isbn hhnv hhcms mpbir2an
      hhph ) ACDAEDZAFDAGRAHDAIJZKDSASLZMABNSABTOPABQP $.
      $( [13-Apr-2008] $) $( [10-Apr-2008] $)
  $}

  ${
    hilcms.1 $e |- D = ( normh o. -h ) $.
    $( The Hilbert space norm determines a complete metric space. $)
    hilcms $p |- D e. CMet $=
      ( cva csm cop cno eqid hhims hhcms ) ACDEFEZJGZAJKBHI $.
      $( [24-Nov-2007] $) $( [17-Apr-2007] $)
  $}

  ${
    $d x y z $.
    $( The Hilbert space of the Hilbert Space Explorer is a complex Hilbert
       space.  (Contributed by Steve Rodriguez, 29-Apr-2007.) $)
    hilhl $p |- <. <. +h , .h >. , normh >. e. CHil $=
      ( cva csm cop cno eqid hhhl ) ABCDCZGEF $.
      $( [29-Apr-2007] $)
  $}

