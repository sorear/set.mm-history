$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Relate Hilbert space to ZFC pre-Hilbert and Hilbert spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f j k t u v w x y z D $.
    hhcms.1 $e |- U = <. <. +h , .h >. , normh >. $.
    hhcms.2 $e |- D = ( IndMet ` U ) $.
    $( The Hilbert space induced metric determines a complete metric space. $)
    hhcms $p |- D e. CMet $=
      ( vx vy vj vk chil cv cfv wcel cn wa wbr wrex clt wi wral cr vf hhmet cca
      hhmetba wf chli clm simpr cc0 cle cmv co cno wb c1 1z nnuz iscauf ffvelrn
      cme mpan adantr adantlr hhmetdval normsub breq1d syl11anc imbi2d ralbidva
      eqtrd rexbidva ralbidv bitrd ccau hcau2 ax-hcompl sylbir visset hlimconvi
      biimpac wceq hlimseqi sylan hlimveci rexbidv mpbird lmbrf2 mp3an1 syl5ibr
      ancoms reximdva sylc iscms2i ) EAUAIABCDUDZABCDUBZUAJZAUCKLZMIWPUEZNZWRWP
      EJZUFOZEIPZWPWTAUGKOZEIPWQWRUHZWSWRUIFJZQOZGJZHJZUJOZXHWPKZXGWPKZUKULUMKZ
      XEQOZRZHMSZGMPZRZFTSZXBXDWRWQXRWRWQXFXIXKXJAULZXEQOZRZHMSZGMPZRZFTSZXRAUT
      LZWRWQYEUNWOFAGHWPUOIMWNUPUQURVAWRYDXQFTWRYCXPXFWRYBXOGMWRXGMLZNZYAXNHMYH
      XHMLZNZXTXMXIYJXKILZXJILZXTXMUNYHYKYIMIXGWPUSVBWRYIYLYGMIXHWPUSZVCYKYLNZX
      SXLXEQYNXSXKXJUKULUMKXLXKXJABCDVDXKXJVEVJVFVGVHVIVKVHVLVMVTWRXRNWPVNLXBFG
      HWPVOEWPVPVQVGWRXAXCEIXAXCWRWTILZNXFXIXJWTAULZXEQOZRZHMSZGMPZRZFTSZXAUUBX
      FXIXJWTUKULUMKZXEQOZRZHMSZGMPZRZFTSFGHWTWPUAVRZEVRZVSXAUUAUUHFTXAYTUUGXFX
      AYSUUFGMXAYRUUEHMXAYINZYQUUDXIUUKYPUUCXEQUUKYLYOYPUUCWAXAWRYIYLWTWPUUIUUJ
      WBYMWCXAYOYIWTWPUUIUUJWDVBXJWTABCDVDVGVFVHVIWEVHVLWFYOWRXCUUBUNZYFYOWRUUL
      WOFAWTGHWPUOIMWNUPUQWGWHWJWIWKWLWM $.
      $( [10-Apr-2008] $)
  $}

  ${
    hhhl.1 $e |- U = <. <. +h , .h >. , normh >. $.
    $( The Hilbert space structure is a complex Hilbert space. $)
    hhhl $p |- U e. CHil $=
      ( chl wcel cbn cphl ishl cnv cims cfv eqid isbn hhnv hhcms mpbir2an hhph
      cms ) ACDAEDZAFDAGRAHDAIJZQDSASKZLABMSABTNOABPO $.
      $( [10-Apr-2008] $)
  $}

  ${
    hilcms.1 $e |- D = ( normh o. -h ) $.
    $( The Hilbert space norm determines a complete metric space. $)
    hilcms $p |- D e. CMet $=
      ( cva csm cop cno eqid hhims hhcms ) ACDEFEZJGZAJKBHI $.
      $( [17-Apr-2007] $)
  $}

  ${
    $d x y z $.
    $( The Hilbert space of the Hilbert Space Explorer is a complex Hilbert
       space.  (Contributed by Steve Rodriguez, 29-Apr-2007.) $)
    hilhl $p |- <. <. +h , .h >. , normh >. e. CHil $=
      ( cva csm cop cno eqid hhhl ) ABCDCZGEF $.
      $( [29-Apr-2007] $)
  $}

