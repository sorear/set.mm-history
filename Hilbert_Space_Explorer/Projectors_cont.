$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Projectors (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A $.  $d x y H $.
    pjtheu2.1 $e |- H e. CH $.
    $( Uniqueness of ` y ` for the projection theorem. $)
    pjtheu2i $p |- ( A e. ~H
        -> E! y e. ( _|_ ` H ) E. x e. H A = ( x +h y ) ) $=
      ( chil wcel cv cva co wceq cort cfv wrex wreu choccli pjtheui wb ococi
      rexeq ax-mp wa cheli ax-hvcom syl2an eqeq2d rexbidva syl5bb reubiia
      sylib ) CFGCBHZAHZIJZKZADLMZLMZNZBUOOCULUKIJZKZADNZBUOOBACUODEPZQUQUTBUOU
      QUNADNZUKUOGZUTUPDKUQVBRDESUNAUPDTUAVCUNUSADVCULDGZUBUMURCVCUKFGULFGUMURK
      VDUKUOVAUCULDEUCUKULUDUEUFUGUHUIUJ $.
      $( [13-Apr-2008] $) $( [6-Nov-1999] $)
  $}

  ${
    pjcl.1 $e |- H e. CH $.
    $( Closure of a projection in its subspace. $)
    pjcli $p |- ( A e. ~H -> ( ( proj ` H ) ` A ) e. H ) $=
      ( cch wcel chil cpj cfv axpjcl mpan ) BDEAFEABGHHBECABIJ $.
      $( [7-Oct-2000] $)

    $( Closure of a projection in Hilbert space. $)
    pjhcli $p |- ( A e. ~H -> ( ( proj ` H ) ` A ) e. ~H ) $=
      ( cch wcel chil cpj cfv pjhcl mpan ) BDEAFEABGHHFECABIJ $.
      $( [7-Oct-2000] $)
  $}

  ${
    $d x y A $.  $d x y H $.
    pjcli.1 $e |- H e. CH $.
    pjcli.2 $e |- A e. ~H $.
    $( Closure of a projection in its subspace. $)
    pjclii $p |- ( ( proj ` H ) ` A ) e. H $=
      ( chil wcel cpj cfv pjcli ax-mp ) AEFABGHHBFDABCIJ $.
      $( [30-Oct-1999] $)

    $( Closure of a projection in Hilbert space. $)
    pjhclii $p |- ( ( proj ` H ) ` A ) e. ~H $=
      ( chil wcel cpj cfv pjhcli ax-mp ) AEFABGHHEFDABCIJ $.
      $( [30-Oct-1999] $)

    $( Decomposition of a vector into projections. $)
    pjpj0i $p |- A = ( ( ( proj ` H ) ` A ) +h
                ( ( proj ` ( _|_ ` H ) ) ` A ) ) $=
      ( vx vy cv wcel cort cfv wa cpj cva co wceq wex wrex r2ex reeanv bitr3i
      crab cuni cch chil choccli pjval mp2an eqcomi wreu wb pjclii pjtheui
      ax-mp opreq1 eqeq2d rexbidv reuuni2 mpbir ococi rexeq cheli pjhclii
      ax-hvcom mpan2 syl rexbiia mpbir2an wi jctr jctl chocunii syl2an imp
      simpld biimpcd ad2antrl mpd 19.23aivv ) EGZBHZFGZBIJZHZKZAVSAWBLJJZMNZOZA
      ABLJJZWAMNZOZKZKZFPEPZAWHWEMNZOZWMWGEBQZWJFWBQZWMWKFWBQEBQWPWQKWKEFBWBRWG
      WJEFBWBSTWPAWEVSMNZOZEWBIJZQZXAAWAVSMNZOZEWTQZFWBUAUBZWEOZWEXEWBUCHAUDHZW
      EXEOBCUEZDFEAWBUFUGUHWEWBHZXDFWBUIZXAXFUJAWBXHDUKZXGXJDFEAWBXHULUMXDXAFWB
      WEWAWEOZXCWSEWTXLXBWRAWAWEVSMUNUOUPUQUGURWPWGEWTQZXAWTBOXMWPUJBCUSWGEWTBU
      TUMWGWSEWTVSWTHVSUDHZWGWSUJVSWTWBXHUEVAXNWFWRAXNWEUDHWFWROAWBXHDVBVSWEVCV
      DUOVEVFTURWQAVSWAMNZOZFWBQZEBUAUBZWHOZWHXRBUCHXGWHXROCDEFABUFUGUHWHBHZXQE
      BUIZWQXSUJABCDUKZXGYADEFABCULUMXQWQEBWHVSWHOZXPWJFWBYCXOWIAVSWHWAMUNUOUPU
      QUGURVGWLWOEFWLYCWOWLYCWEWAOZWDWKYCYDKZVTVTXIKXTWCKWKYEVHWCVTXIXKVIWCXTYB
      VJVSWEWHWAABCVKVLVMVNWGYCWOVHWDWJYCWGWOYCWFWNAVSWHWEMUNUOVOVPVQVRUM $.
      $( [26-Oct-1999] $)
  $}

  $( Decomposition of a vector into projections.  See comment in ~ axpjcl . $)
  axpjpj $p |- ( ( H e. CH /\ A e. ~H ) ->
            A = ( ( ( proj ` H ) ` A ) +h ( ( proj ` ( _|_ ` H ) ) ` A ) ) ) $=
    ( cch wcel chil cpj cfv cort cva co wceq cif c0v fveq2 fveq1d fveq2d
    opreq12d eqeq2d id eqeq12d helch elimel ax-hv0cl pjpj0i dedth2h ) BCDZAEDZA
    ABFGZGZABHGZFGZGZIJZKAAUFBELZFGZGZAUNHGZFGZGZIJZKUGAMLZVAUOGZVAURGZIJZKBAEM
    BUNKZUMUTAVEUIUPULUSIVEAUHUOBUNFNOVEAUKURVEUJUQFBUNHNPOQRAVAKZAVAUTVDVFSVFU
    PVBUSVCIAVAUONAVAURNQTVAUNBECUAUBAMEUCUBUDUE $.
    $( [26-Oct-1999] $)

$(
  @( Decomposition of a vector into projections, as an axiom.  Can be used to
     replace ~ axpjpj in studies of the Hilbert space completeness axiom. @)
  ax-pjpj @a |- ( ( H e. CH /\ A e. ~H ) ->
            A = ( ( ( proj ` H ) ` A ) +h ( ( proj ` ( _|_ ` H ) ) ` A ) ) ) @.
$)

  ${
    $d x y A $.  $d x y H $.
    pjpj.1 $e |- H e. CH $.
    pjpj.2 $e |- A e. ~H $.
    $( Decomposition of a vector into projections. $)
    pjpji $p |- A = ( ( ( proj ` H ) ` A ) +h
               ( ( proj ` ( _|_ ` H ) ) ` A ) ) $=
      ( pjpj0i ) ABCDE $.
      $( [6-Nov-1999] $)
  $}

  ${
    $d x y A $.  $d x y H $.
    $( Projection Theorem:  Any Hilbert space vector ` A ` can be decomposed
       into a member ` x ` of a closed subspace ` H ` and a member ` y ` of the
       complement of the subspace.  Theorem 3.7(i) of [Beran] p. 102 (existence
       part). $)
    pjpjth $p |- ( ( H e. CH /\ A e. ~H ) ->
                E. x e. H E. y e. ( _|_ ` H ) A = ( x +h y ) ) $=
      ( cch wcel chil wa cpj cfv cort cva co wceq cv wrex axpjcl choccl sylan
      axpjpj rcla4eopr syl111anc ) DEFZCGFZHCDIJJZDFCDKJZIJJZUFFZCUEUGLMNCAOBOL
      MNBUFPADPCDQUCUFEFUDUHDRCUFQSCDTABDUFUEUGCLUAUB $.
      $( [6-Nov-1999] $)
  $}

  ${
    $d x y A $.  $d x y H $.
    pjpjth.1 $e |- A e. ~H $.
    pjpjth.2 $e |- H e. CH $.
    $( Projection Theorem:  Any Hilbert space vector ` A ` can be decomposed
       into a member ` x ` of a closed subspace ` H ` and a member ` y ` of the
       complement of the subspace.  Theorem 3.7(i) of [Beran] p. 102 (existence
       part). $)
    pjpjthi $p |- E. x e. H E. y e. ( _|_ ` H ) A = ( x +h y ) $=
      ( cch wcel chil cv cva co wceq cort cfv wrex pjpjth mp2an ) DGHCIHCAJBJKL
      MBDNOPADPFEABCDQR $.
      $( [6-Nov-1999] $)
  $}

  $( Orthocomplement projection in terms of projection. $)
  pjop $p |- ( ( H e. CH /\ A e. ~H ) ->
           ( ( proj ` ( _|_ ` H ) ) ` A ) = ( A -h ( ( proj ` H ) ` A ) ) ) $=
    ( cch wcel chil wa cpj cfv cmv co cort wceq cva axpjpj eqcomd wb simpr
    pjhcl choccl sylan hvsubadd syl111anc mpbird ) BCDZAEDZFZAABGHHZIJZABKHZGHH
    ZUFUHUJLZUGUJMJZALZUFAULABNOUFUEUGEDUJEDZUKUMPUDUEQABRUDUICDUEUNBSAUIRTAUGU
    JUAUBUCO $.
    $( [5-Nov-1999] $)

  $( Projection in terms of orthocomplement projection. $)
  pjpo $p |- ( ( H e. CH /\ A e. ~H ) ->
           ( ( proj ` H ) ` A ) = ( A -h ( ( proj ` ( _|_ ` H ) ) ` A ) ) ) $=
    ( cch wcel chil wa cort cfv cpj cmv co wceq cva choccl pjhcl sylan
    ax-hvcom syl11anc axpjpj eqtr4d wb simpr hvsubadd syl111anc mpbird eqcomd
    ) BCDZAEDZFZAABGHZIHHZJKZABIHHZUIULUMLZUKUMMKZALZUIUOUMUKMKZAUIUKEDZUMEDZUO
    UQLUGUJCDUHURBNAUJOPZABOZUKUMQRABSTUIUHURUSUNUPUAUGUHUBUTVAAUKUMUCUDUEUF $.
    $( [5-Nov-1999] $)

  ${
    pjop.1 $e |- H e. CH $.
    pjop.2 $e |- A e. ~H $.
    $( Orthocomplement projection in terms of projection. $)
    pjopi $p |- ( ( proj ` ( _|_ ` H ) ) ` A ) =
               ( A -h ( ( proj ` H ) ` A ) ) $=
      ( cch wcel chil cort cfv cpj cmv co wceq pjop mp2an ) BEFAGFABHIJIIAABJII
      KLMCDABNO $.
      $( [31-Oct-1999] $)

    $( Projection in terms of orthocomplement projection. $)
    pjpoi $p |- ( ( proj ` H ) ` A ) =
               ( A -h ( ( proj ` ( _|_ ` H ) ) ` A ) ) $=
      ( cch wcel chil cpj cfv cort cmv co wceq pjpo mp2an ) BEFAGFABHIIAABJIHII
      KLMCDABNO $.
      $( [31-Oct-1999] $)

    $( Projection of a vector in the orthocomplement of the projection
       subspace. $)
    pjoc1i $p |- ( A e. H <-> ( ( proj ` ( _|_ ` H ) ) ` A ) = 0h ) $=
      ( wcel cort cfv cpj c0v wceq c0h cin wa cmv co pjopi pjclii csh wi
      chshii shsubclOLD ax-mp mpan2 syl5eqel choccli jctir elin sylibr ocin
      syl6eleq elch0 sylib cva pjpji opreq2 syl5eq chil pjhclii ax-hvaddid
      syl6eq syl6eqel impbii ) ABEZABFGZHGGZIJZVCVEKEVFVCVEBVDLZKVCVEBEZVEVDEZM
      VEVGEVCVHVIVCVEAABHGGZNOZBABCDPVCVJBEZVKBEZABCDQZBREZVCVLMVMSBCTZAVJBUAUB
      UCUDAVDBCUEDQUFVEBVDUGUHVOVGKJVPBUIUBUJVEUKULVFAVJBVFAVJIUMOZVJVFAVJVEUMO
      VQABCDUNVEIVJUMUOUPVJUQEVQVJJABCDURVJUSUBUTVNVAVB $.
      $( [27-Oct-1999] $)

    $( Projection of a vector in the projection subspace.  Lemma 4.4(ii) of
       [Beran] p. 111. $)
    pjchi $p |- ( A e. H <-> ( ( proj ` H ) ` A ) = A ) $=
      ( wcel cpj cfv wceq c0v cva co chil pjhclii ax-hvaddid ax-mp cort pjpji
      pjoc1i biimpi opreq2d syl5req syl5eqr pjclii eleq1 mpbii impbii ) ABEZABF
      GGZAHZUGUHUHIJKZAUHLEUJUHHABCDMUHNOUGAUHABPGFGGZJKUJABCDQUGUKIUHJUGUKIHAB
      CDRSTUAUBUIUHBEUGABCDUCUHABUDUEUF $.
      $( [27-Oct-1999] $)
  $}

  $( The part of a vector that belongs to the orthocomplemented space. $)
  pjoccl $p |- ( ( H e. CH /\ A e. ~H ) ->
          ( A -h ( ( proj ` H ) ` A ) ) e. ( _|_ ` H ) ) $=
    ( cch wcel chil wa cort cfv cpj cmv co pjop choccl axpjcl sylan eqeltrrd )
    BCDZAEDZFABGHZIHHZAABIHHJKSABLQSCDRTSDBMASNOP $.
    $( [11-Apr-2006] $)

  $( Projection of a vector in the orthocomplement of the projection
     subspace. $)
  pjoc1 $p |- ( ( H e. CH /\ A e. ~H ) ->
               ( A e. H <-> ( ( proj ` ( _|_ ` H ) ) ` A ) = 0h ) ) $=
    ( cch wcel chil cort cfv cpj c0v wceq wb cif eleq2 fveq2 fveq2d fveq1d
    eqeq1d bibi12d eleq1 helch elimel ax-hv0cl pjoc1i dedth2h ) BCDZAEDZABDZABF
    GZHGZGZIJZKAUEBELZDZAULFGZHGZGZIJZKUFAILZULDZURUOGZIJZKBAEIBULJZUGUMUKUQBUL
    AMVBUJUPIVBAUIUOVBUHUNHBULFNOPQRAURJZUMUSUQVAAURULSVCUPUTIAURUONQRURULBECTU
    AAIEUBUAUCUD $.
    $( [6-Nov-1999] $)

  ${
    $d x A $.  $d x B $.
    pjoml.1 $e |- A e. CH $.
    pjoml.2 $e |- B e. SH $.
    $( Subspace form of orthomodular law in the Hilbert lattice.  Compare the
       orthomodular law in Theorem 2(ii) of [Kalmbach] p. 22.  Derived using
       projections; compare ~ omlsi . $)
    pjomli $p |- ( ( A C_ B /\ ( B i^i ( _|_ ` A ) ) = 0H ) -> A = B ) $=
      ( omlsi ) ABCDE $.
      $( [6-Nov-1999] $)
  $}

  $( Subspace form of orthomodular law in the Hilbert lattice.  Compare the
     orthomodular law in Theorem 2(ii) of [Kalmbach] p. 22.  Derived using
     projections; compare ~ omlsi . $)
  pjoml $p |- ( ( ( A e. CH /\ B e. SH ) /\
               ( A C_ B /\ ( B i^i ( _|_ ` A ) ) = 0H ) ) -> A = B ) $=
    ( cch wcel csh wa wss cort cfv cin c0h wceq wi cif sseq1 fveq2 ineq2d
    eqeq1d anbi12d eqeq1 imbi12d sseq2 ineq1 eqeq2 h0elch elimel h0elsh pjomli
    dedth2h imp ) ACDZBEDZFABGZBAHIZJZKLZFZABLZUKULUQURMUKAKNZBGZBUSHIZJZKLZFZU
    SBLZMUSULBKNZGZVFVAJZKLZFZUSVFLZMABKKAUSLZUQVDURVEVLUMUTUPVCAUSBOVLUOVBKVLU
    NVABAUSHPQRSAUSBTUABVFLZVDVJVEVKVMUTVGVCVIBVFUSUBVMVBVHKBVFVAUCRSBVFUSUDUAU
    SVFAKCUEUFBKEUGUFUHUIUJ $.
    $( [14-Jun-2006] $)

  ${
    pjococ.1 $e |- H e. CH $.
    $( Proof of orthocomplement theorem using projections.  Compare ~ ococ . $)
    pjococi $p |- ( _|_ ` ( _|_ ` H ) ) = H $=
      ( ococi ) ABC $.
      $( [5-Nov-1999] $)
  $}

  ${
    pjoc2.1 $e |- H e. CH $.
    pjoc2.2 $e |- A e. ~H $.
    $( Projection of a vector in the orthocomplement of the projection
       subspace.  Lemma 4.4(iii) of [Beran] p. 111. $)
    pjoc2i $p |- ( A e. ( _|_ ` H ) <-> ( ( proj ` H ) ` A ) = 0h ) $=
      ( cort cfv wcel cpj c0v wceq choccli pjoc1i pjococi fveq2i fveq1i eqeq1i
      bitri ) ABEFZGAREFZHFZFZIJABHFZFZIJARBCKDLUAUCIATUBSBHBCMNOPQ $.
      $( [27-Oct-1999] $)
  $}

  $( Projection of a vector in the orthocomplement of the projection subspace.
     Lemma 4.4(iii) of [Beran] p. 111. $)
  pjoc2 $p |- ( ( H e. CH /\ A e. ~H ) ->
               ( A e. ( _|_ ` H ) <-> ( ( proj ` H ) ` A ) = 0h ) ) $=
    ( cch wcel chil cort cfv cpj c0v wceq wb c0h cif fveq2 eleq2d fveq1d
    eqeq1d bibi12d eleq1 h0elch elimel ax-hv0cl pjoc2i dedth2h ) BCDZAEDZABFGZD
    ZABHGZGZIJZKAUEBLMZFGZDZAULHGZGZIJZKUFAIMZUMDZURUOGZIJZKBALIBULJZUHUNUKUQVB
    UGUMABULFNOVBUJUPIVBAUIUOBULHNPQRAURJZUNUSUQVAAURUMSVCUPUTIAURUONQRURULBLCT
    UAAIEUBUAUCUD $.
    $( [24-Apr-2006] $)

