$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Projectors (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    pjcl.1 $e |- H e. CH $.
    $( Closure of a projection in its subspace. $)
    pjcl $p |- ( A e. H~ -> ( ( proj ` H ) ` A ) e. H ) $=
      ( cch wcel chil cpj cfv axpjclt mpan ) BDEAFEABGHHBECABIJ $.
      $( [7-Oct-2000] $)

    $( Closure of a projection in Hilbert space. $)
    pjhcl $p |- ( A e. H~ -> ( ( proj ` H ) ` A ) e. H~ ) $=
      ( cch wcel chil cpj cfv pjhclt mpan ) BDEAFEABGHHFECABIJ $.
      $( [7-Oct-2000] $)
  $}

  ${
    $d x y A $.  $d x y H $.
    pjcli.1 $e |- H e. CH $.
    pjcli.2 $e |- A e. H~ $.
    $( Closure of a projection in its subspace. $)
    pjcli $p |- ( ( proj ` H ) ` A ) e. H $=
      ( chil wcel cpj cfv pjcl ax-mp ) AEFABGHHBFDABCIJ $.
      $( [30-Oct-1999] $)

    $( Closure of a projection in Hilbert space. $)
    pjhcli $p |- ( ( proj ` H ) ` A ) e. H~ $=
      ( chil wcel cpj cfv pjhcl ax-mp ) AEFABGHHEFDABCIJ $.
      $( [30-Oct-1999] $)

    $( Decomposition of a vector into projections. $)
    pjpj0 $p |- A = ( ( ( proj ` H ) ` A ) +h
                ( ( proj ` ( _|_ ` H ) ) ` A ) ) $=
      ( vx vy cv wcel cort cfv wa cpj cva co wceq wex wrex r2ex reeanv bitr3
      crab cuni cch chil choccl pjvalt mp2an eqcomi wreu wb pjcli pjthu ax-mp
      opreq1 eqeq2d rexbidv reuuni2 mpbir ococ rexeq1 chel pjhcli ax-hvcom
      mpan2 syl rexbiia mpbir2an wi chocuni jctr jctl syl2an imp pm3.26d
      biimpcd ad2antrl mpd 19.23aivv ) EGZBHZFGZBIJZHZKZAVSAWBLJJZMNZOZAABLJJZW
      AMNZOZKZKZFPEPZAWHWEMNZOZWMWGEBQZWJFWBQZWMWKFWBQEBQWPWQKWKEFBWBRWGWJEFBWB
      STWPAWEVSMNZOZEWBIJZQZXAAWAVSMNZOZEWTQZFWBUAUBZWEOZWEXEWBUCHAUDHZWEXEOBCU
      EZDFEAWBUFUGUHWEWBHZXDFWBUIZXAXFUJAWBXHDUKZXGXJDFEAWBXHULUMXDXAFWBWEWAWEO
      ZXCWSEWTXLXBWRAWAWEVSMUNUOUPUQUGURWPWGEWTQZXAWTBOXMWPUJBCUSWGEWTBUTUMWGWS
      EWTVSWTHVSUDHZWGWSUJVSWTWBXHUEVAXNWFWRAXNWEUDHWFWROAWBXHDVBVSWEVCVDUOVEVF
      TURWQAVSWAMNZOZFWBQZEBUAUBZWHOZWHXRBUCHXGWHXROCDEFABUFUGUHWHBHZXQEBUIZWQX
      SUJABCDUKZXGYADEFABCULUMXQWQEBWHVSWHOZXPWJFWBYCXOWIAVSWHWAMUNUOUPUQUGURVG
      WLWOEFWLYCWOWLYCWEWAOZWDWKYCYDKZVTXIKXTWCKWKYEVHVTWCVSWEWHWAABCVIVTXIXKVJ
      WCXTYBVKVLVMVNWGYCWOVHWDWJYCWGWOYCWFWNAVSWHWEMUNUOVOVPVQVRUM $.
      $( [26-Oct-1999] $)
  $}

  $( Decomposition of a vector into projections.  See comment in ~ axpjclt . $)
  axpjpjt $p |- ( ( H e. CH /\ A e. H~ ) ->
            A = ( ( ( proj ` H ) ` A ) +h ( ( proj ` ( _|_ ` H ) ) ` A ) ) ) $=
    ( cch wcel chil cpj cfv cort cva co wceq cif c0v fveq2 fveq1d fveq2d
    opreq12d eqeq2d id eqeq12d helch elimel ax-hv0cl pjpj0 dedth2h ) BCDZAEDZAA
    BFGZGZABHGZFGZGZIJZKAAUFBELZFGZGZAUNHGZFGZGZIJZKUGAMLZVAUOGZVAURGZIJZKBAEMB
    UNKZUMUTAVEUIUPULUSIVEAUHUOBUNFNOVEAUKURVEUJUQFBUNHNPOQRAVAKZAVAUTVDVFSVFUP
    VBUSVCIAVAUONAVAURNQTVAUNBECUAUBAMEUCUBUDUE $.
    $( [26-Oct-1999] $)

$(
  @( Decomposition of a vector into projections, as an axiom.  Can be used to
     replace ~ axpjpjt in studies of the Hilbert space completeness axiom. @)
  ax-pjpj @a |- ( ( H e. CH /\ A e. H~ ) ->
            A = ( ( ( proj ` H ) ` A ) +h ( ( proj ` ( _|_ ` H ) ) ` A ) ) ) @.
$)

  ${
    $d x y A $.  $d x y H $.
    pjpj.1 $e |- H e. CH $.
    pjpj.2 $e |- A e. H~ $.
    $( Decomposition of a vector into projections. $)
    pjpj $p |- A = ( ( ( proj ` H ) ` A ) +h
               ( ( proj ` ( _|_ ` H ) ) ` A ) ) $=
      ( pjpj0 ) ABCDE $.
      $( [6-Nov-1999] $)
  $}

  ${
    $d x y A $.  $d x y H $.
    $( Projection Theorem:  Any Hilbert space vector ` A ` can be decomposed
       into a member ` x ` of a closed subspace ` H ` and a member ` y` of the
       complement of the subspace. Theorem 3.7(i) of [Beran] p. 102 (existence
       part). $)
    pjpjtht $p |- ( ( H e. CH /\ A e. H~ ) ->
                E. x e. H E. y e. ( _|_ ` H ) A = ( x +h y ) ) $=
      ( cpj cfv wcel cort cva co wceq cv wrex cch chil wa rcla4eopr axpjclt
      chocclt sylan axpjpjt syl3anc ) CDEFFZDGCDHFZEFFZUDGZCUCUEIJKCALBLIJKBUDM
      ADMDNGZCOGZPABDUDUCUECIQCDRUDNGUHUFUGCUDRDSTCDUAUB $.
      $( [6-Nov-1999] $)
  $}

  ${
    $d x y A $.  $d x y H $.
    pjpjth.1 $e |- A e. H~ $.
    pjpjth.2 $e |- H e. CH $.
    $( Projection Theorem:  Any Hilbert space vector ` A ` can be decomposed
       into a member ` x ` of a closed subspace ` H ` and a member ` y` of the
       complement of the subspace. Theorem 3.7(i) of [Beran] p. 102 (existence
       part). $)
    pjpjth $p |- E. x e. H E. y e. ( _|_ ` H ) A = ( x +h y ) $=
      ( cch wcel chil cv cva co wceq cort cfv wrex pjpjtht mp2an ) DGHCIHCAJBJK
      LMBDNOPADPFEABCDQR $.
      $( [6-Nov-1999] $)
  $}

  $( Orthocomplement projection in terms of projection. $)
  pjopt $p |- ( ( H e. CH /\ A e. H~ ) ->
           ( ( proj ` ( _|_ ` H ) ) ` A ) = ( A -h ( ( proj ` H ) ` A ) ) ) $=
    ( cch wcel chil wa cpj cfv cmv co cort wceq cva axpjpjt eqcomd wb
    hvsubaddt pm3.27 pjhclt chocclt sylan syl3anc mpbird ) BCDZAEDZFZAABGHHZIJZ
    ABKHZGHHZUFUHUJLZUGUJMJZALZUFAULABNOUEUGEDUJEDZUKUMPUFAUGUJQUDUERABSUICDUEU
    NUDAUISBTUAUBUCO $.
    $( [5-Nov-1999] $)

  $( Projection in terms of orthocomplement projection. $)
  pjpot $p |- ( ( H e. CH /\ A e. H~ ) ->
           ( ( proj ` H ) ` A ) = ( A -h ( ( proj ` ( _|_ ` H ) ) ` A ) ) ) $=
    ( cch wcel chil wa cort cfv cpj cmv co wceq cva ax-hvcom pjhclt chocclt
    sylan sylanc axpjpjt eqtr4d wb hvsubaddt pm3.27 syl3anc mpbird eqcomd ) BCD
    ZAEDZFZAABGHZIHHZJKZABIHHZUIULUMLZUKUMMKZALZUIUOUMUKMKZAUKEDZUMEDZUOUQLUIUK
    UMNUJCDUHURUGAUJOBPQZABOZRABSTUHURUSUNUPUAUIAUKUMUBUGUHUCUTVAUDUEUF $.
    $( [5-Nov-1999] $)

  ${
    pjop.1 $e |- H e. CH $.
    pjop.2 $e |- A e. H~ $.
    $( Orthocomplement projection in terms of projection. $)
    pjop $p |- ( ( proj ` ( _|_ ` H ) ) ` A ) =
               ( A -h ( ( proj ` H ) ` A ) ) $=
      ( cch wcel chil cort cfv cpj cmv co wceq pjopt mp2an ) BEFAGFABHIJIIAABJI
      IKLMCDABNO $.
      $( [31-Oct-1999] $)

    $( Projection in terms of orthocomplement projection. $)
    pjpo $p |- ( ( proj ` H ) ` A ) =
               ( A -h ( ( proj ` ( _|_ ` H ) ) ` A ) ) $=
      ( cch wcel chil cpj cfv cort cmv co wceq pjpot mp2an ) BEFAGFABHIIAABJIHI
      IKLMCDABNO $.
      $( [31-Oct-1999] $)

    $( Projection of a vector in the orthocomplement of the projection
       subspace. $)
    pjoc1 $p |- ( A e. H <-> ( ( proj ` ( _|_ ` H ) ) ` A ) = 0h ) $=
      ( wcel cort cfv cpj c0v wceq c0h cin wa cmv co pjcli csh wi chshi
      shsubclt ax-mp mpan2 pjop syl5eqel choccl jctir elin sylibr ocin
      syl6eleq elch0 sylib cva opreq2 pjpj syl5eq chil pjhcli ax-hvaddid
      syl6eq syl6eqel impbi ) ABEZABFGZHGGZIJZVCVEKEVFVCVEBVDLZKVCVEBEZVEVDEZMV
      EVGEVCVHVIVCAABHGGZNOZBVEVCVJBEZVKBEZABCDPZBQEZVCVLMVMRBCSZAVJBTUAUBABCDU
      CUDAVDBCUEDPUFVEBVDUGUHVOVGKJVPBUIUAUJVEUKULVFAVJBVFAVJIUMOZVJVFVJVEUMOVQ
      AVEIVJUMUNABCDUOUPVJUQEVQVJJABCDURVJUSUAUTVNVAVB $.
      $( [27-Oct-1999] $)

    $( Projection of a vector in the projection subspace.  Lemma 4.4(ii) of
       [Beran] p. 111. $)
    pjch $p |- ( A e. H <-> ( ( proj ` H ) ` A ) = A ) $=
      ( wcel cpj cfv wceq c0v cva co cort pjoc1 biimp opreq2d pjpj syl5req
      chil pjhcli ax-hvaddid ax-mp syl5eqr pjcli eleq1 mpbii impbi ) ABEZABFGGZ
      AHZUGUHIJKZAUHUGUHABLGFGGZJKUJAUGUKIUHJUGUKIHABCDMNOABCDPQUHREUJUHHABCDSU
      HTUAUBUIUHBEUGABCDUCUHABUDUEUF $.
      $( [27-Oct-1999] $)
  $}

  $( The part of a vector that belongs to the orthocomplemented space. $)
  pjocclt $p |- ( ( H e. CH /\ A e. H~ ) ->
          ( A -h ( ( proj ` H ) ` A ) ) e. ( _|_ ` H ) ) $=
    ( cch wcel chil wa cort cfv cpj cmv co pjopt axpjclt chocclt sylan
    eqeltrrd ) BCDZAEDZFABGHZIHHZAABIHHJKSABLSCDRTSDQASMBNOP $.
    $( [11-May-2006] $) $( [11-Apr-2006] $)

  $( Projection of a vector in the orthocomplement of the projection
     subspace. $)
  pjoc1t $p |- ( ( H e. CH /\ A e. H~ ) ->
               ( A e. H <-> ( ( proj ` ( _|_ ` H ) ) ` A ) = 0h ) ) $=
    ( cch wcel chil cort cfv cpj c0v wceq wb cif eleq2 fveq2 fveq2d fveq1d
    eqeq1d bibi12d eleq1 helch elimel ax-hv0cl pjoc1 dedth2h ) BCDZAEDZABDZABFG
    ZHGZGZIJZKAUEBELZDZAULFGZHGZGZIJZKUFAILZULDZURUOGZIJZKBAEIBULJZUGUMUKUQBULA
    MVBUJUPIVBAUIUOVBUHUNHBULFNOPQRAURJZUMUSUQVAAURULSVCUPUTIAURUONQRURULBECTUA
    AIEUBUAUCUD $.
    $( [6-Nov-1999] $)

  ${
    $d x A $.  $d x B $.
    pjoml.1 $e |- A e. CH $.
    pjoml.2 $e |- B e. SH $.
    $( Subspace form of orthomodular law in the Hilbert lattice.  Compare the
       orthomodular law in Theorem 2(ii) of [Kalmbach] p. 22.   Derived using
       projections; compare ~ omls . $)
    pjoml $p |- ( ( A (_ B /\ ( B i^i ( _|_ ` A ) ) = 0H ) -> A = B ) $=
      ( omls ) ABCDE $.
      $( [6-Nov-1999] $)
  $}

  $( Subspace form of orthomodular law in the Hilbert lattice.  Compare the
     orthomodular law in Theorem 2(ii) of [Kalmbach] p. 22.   Derived using
     projections; compare ~ omls . $)
  pjomlt $p |- ( ( ( A e. CH /\ B e. SH ) /\
               ( A (_ B /\ ( B i^i ( _|_ ` A ) ) = 0H ) ) -> A = B ) $=
    ( cch wcel csh wa wss cort cfv cin c0h wceq wi cif sseq1 fveq2 ineq2d
    eqeq1d anbi12d eqeq1 imbi12d sseq2 ineq1 eqeq2 h0elch elimel h0elsh pjoml
    dedth2h imp ) ACDZBEDZFABGZBAHIZJZKLZFZABLZUKULUQURMUKAKNZBGZBUSHIZJZKLZFZU
    SBLZMUSULBKNZGZVFVAJZKLZFZUSVFLZMABKKAUSLZUQVDURVEVLUMUTUPVCAUSBOVLUOVBKVLU
    NVABAUSHPQRSAUSBTUABVFLZVDVJVEVKVMUTVGVCVIBVFUSUBVMVBVHKBVFVAUCRSBVFUSUDUAU
    SVFAKCUEUFBKEUGUFUHUIUJ $.
    $( [14-Jun-2006] $) $( [14-Jun-2006] $)

  ${
    pjococ.1 $e |- H e. CH $.
    $( Proof of orthocomplement theorem using projections.  Compare ~ococ . $)
    pjococ $p |- ( _|_ ` ( _|_ ` H ) ) = H $=
      ( ococ ) ABC $.
      $( [5-Nov-1999] $)
  $}

  ${
    pjoc2.1 $e |- H e. CH $.
    ${
      pjoc2.2 $e |- A e. H~ $.
      $( Projection of a vector in the orthocomplement of the projection
         subspace.  Lemma 4.4(iii) of [Beran] p. 111. $)
      pjoc2 $p |- ( A e. ( _|_ ` H ) <-> ( ( proj ` H ) ` A ) = 0h ) $=
        ( cort cfv wcel cpj c0v wceq choccl pjoc1 pjococ fveq2i fveq1i eqeq1i
        bitr ) ABEFZGAREFZHFZFZIJABHFZFZIJARBCKDLUAUCIATUBSBHBCMNOPQ $.
        $( [27-Oct-1999] $)
    $}

    $( Projection of a vector in the orthocomplement of the projection
       subspace.  Lemma 4.4(iii) of [Beran] p. 111. $)
    pjoc2tOLD $p |- ( A e. H~ ->
                 ( A e. ( _|_ ` H ) <-> ( ( proj ` H ) ` A ) = 0h ) ) $=
      ( chil wcel cort cfv cpj c0v wceq wb cif eleq1 fveq2 eqeq1d bibi12d
      ax-hv0cl elimel pjoc2 dedth ) ADEZABFGZEZABHGZGZIJZKUAAILZUBEZUGUDGZIJZKA
      IAUGJZUCUHUFUJAUGUBMUKUEUIIAUGUDNOPUGBCAIDQRST $.
      $( [14-Aug-2000] $)
  $}

  $( Projection of a vector in the orthocomplement of the projection
     subspace.  Lemma 4.4(iii) of [Beran] p. 111. $)
  pjoc2t $p |- ( ( H e. CH /\ A e. H~ ) ->
               ( A e. ( _|_ ` H ) <-> ( ( proj ` H ) ` A ) = 0h ) ) $=
    ( cch wcel chil cort cfv cpj c0v wceq wb c0h cif fveq2 eleq2d fveq1d
    eqeq1d bibi12d eleq1 h0elch elimel ax-hv0cl pjoc2 dedth2h ) BCDZAEDZABFGZDZ
    ABHGZGZIJZKAUEBLMZFGZDZAULHGZGZIJZKUFAIMZUMDZURUOGZIJZKBALIBULJZUHUNUKUQVBU
    GUMABULFNOVBUJUPIVBAUIUOBULHNPQRAURJZUNUSUQVAAURUMSVCUPUTIAURUONQRURULBLCTU
    AAIEUBUAUCUD $.
    $( [13-May-2006] $) $( [24-Apr-2006] $)

