$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Orthoarguesian laws 5OA and 3OA
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    5oalem1.1 $e |- A e. SH $.
    5oalem1.2 $e |- B e. SH $.
    5oalem1.3 $e |- C e. SH $.
    5oalem1.4 $e |- R e. SH $.
    $( Lemma for orthoarguesian law 5OA. $)
    5oalem1 $p |- ( ( ( ( x e. A /\ y e. B ) /\ v = ( x +h y ) ) /\
                  ( z e. C /\ ( x -h z ) e. R ) ) ->
                  v e. ( B +H ( A i^i ( C +H R ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin simplll chil sheli ad2antrr adantr
      hvaddsub12 3anidm23 c0v hvsubid opreq2d ax-hvaddid sylan9eqr eqtr3d
      syl2an shsvai adantl eqeltrrd elin sylanbrc simpllr shscli shincli
      shscomi syl6eleq syl11anc wb eleq1 ad2antlr mpbird ) AMZENZBMZFNZOZDMZVKV
      MPQZRZOZCMZGNZVKVTSQZHNZOZOZVPFEGHTQZUAZTQZNZVQWHNZWEVKWGNZVNWJWEVLVKWFNW
      KVLVNVRWDUBWEVTWBPQZVKWFVSVKUCNZVTUCNZWLVKRWDVLWMVNVRVKEIUDUEWAWNWCVTGKUD
      UFWMWNOVKVTVTSQZPQZWLVKWMWNWPWLRVKVTVTUGUHWNWMWPVKUIPQVKWNWOUIVKPVTUJUKVK
      ULUMUNUOWDWLWFNVSGHVTWBKLUPUQURVKEWFUSUTVLVNVRWDVAWKVNOVQWGFTQWHWGFVKVMEW
      FIGHKLVBVCZJUPWGFWQJVDVEVFVRWIWJVGVOWDVPVQWHVHVIVJ $.
      $( [1-Apr-2000] $)
  $}

  ${
    5oalem2.1 $e |- A e. SH $.
    5oalem2.2 $e |- B e. SH $.
    5oalem2.3 $e |- C e. SH $.
    5oalem2.4 $e |- D e. SH $.
    $( Lemma for orthoarguesian law 5OA. $)
    5oalem2 $p |- ( ( ( ( x e. A /\ y e. B ) /\ ( z e. C /\ w e. D ) ) /\
                  ( x +h y ) = ( z +h w ) ) ->
                  ( x -h z ) e. ( ( A +H C ) i^i ( B +H D ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin shsvsi ad2ant2r adantr ancom
      shscomi eleq2i 3imtr4i ad2ant2l chil wb sheli anim12i opreq1 adantl c0v
      simpr anim2i ancoms hvsub4 syldan hvsubid opreq2d ad2antlr hvsubcl
      ax-hvaddid syl adantlr 3eqtrd adantrr simpl anim1i syl11anc opreq1d
      ad2antrl hvaddid2 adantrl adantll 3eqtr3d eleq1d sylan mpbird elin
      sylanbrc ) AMZENZBMZFNZOZCMZGNZDMZHNZOZOZWEWGPQZWJWLPQZRZOZWEWJSQZEGTQZNZ
      WTFHTQZNZWTXAXCUANWOXBWRWFWKXBWHWMEGWEWJIKUBUCUDWSXDWLWGSQZXCNZWOXFWRWHWM
      XFWFWKWMWHOXEHFTQZNWHWMOXFHFWLWGLJUBWHWMUEXCXGXEFHJLUFUGUHUIUDWOWEUJNZWGU
      JNZOZWJUJNZWLUJNZOZOZWRXDXFUKWIXJWNXMWFXHWHXIWEEIULWGFJULUMWKXKWMXLWJGKUL
      WLHLULUMUMXNWROZWTXEXCXOWPWJWGPQZSQZWQXPSQZWTXEWRXQXRRXNWPWQXPSUNUOXNXQWT
      RZWRXJXKXSXLXJXKOXQWTWGWGSQZPQZWTUPPQZWTXJXKXKXIOZXQYARXKXJYCXJXIXKXHXIUQ
      URUSWEWGWJWGUTVAXIYAYBRXHXKXIXTUPWTPWGVBVCVDXHXKYBWTRZXIXHXKOWTUJNYDWEWJV
      EWTVFVGVHVIVJUDXNXRXERZWRXIXMYEXHXIXMOZXRWJWJSQZXEPQZUPXEPQZXEYFXMYCXRYHR
      XIXMUQXMXIYCXMXKXIXKXLVKVLUSWJWLWJWGUTVMXKYHYIRXIXLXKYGUPXEPWJVBVNVOXIXLY
      IXERZXKXLXIYJXLXIOXEUJNYJWLWGVEXEVPVGUSVQVIVRUDVSVTWAWBWTXAXCWCWD $.
      $( [2-Apr-2000] $)
  $}

  ${
    5oalem3.1 $e |- A e. SH $.
    5oalem3.2 $e |- B e. SH $.
    5oalem3.3 $e |- C e. SH $.
    5oalem3.4 $e |- D e. SH $.
    5oalem3.5 $e |- F e. SH $.
    5oalem3.6 $e |- G e. SH $.
    $( Lemma for orthoarguesian law 5OA. $)
    5oalem3 $p |- ( ( ( ( ( x e. A /\ y e. B ) /\ ( z e. C /\ w e. D ) )
                    /\ ( f e. F /\ g e. G ) ) /\
                  ( ( x +h y ) = ( f +h g ) /\ ( z +h w ) = ( f +h g ) ) ) ->
                  ( x -h z ) e. ( ( ( A +H F ) i^i ( B +H G ) ) +H
                    ( ( C +H F ) i^i ( D +H G ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin anandir 5oalem2 anim12i an4s sylanb
      shscli shincli shsvsi syl wb chil sheli adantr c0v hvsubsub4 anandirs
      hvsubid opreq2d adantl hvsubcl hvsub0 3eqtrd syl2an eleq1d mpbid ) ASZETZ
      BSZFTZUAZCSZGTZDSZHTZUAZUAZISZKTZJSZLTZUAZUAZVMVOUBUCWDWFUBUCZUDZVRVTUBUC
      WJUDZUAZUAZVMWDUEUCZVRWDUEUCZUEUCZEKUFUCZFLUFUCZUGZGKUFUCZHLUFUCZUGZUFUCZ
      TZVMVRUEUCZXDTZWNWOWTTZWPXCTZUAZXEWIVQWHUAZWBWHUAZUAWMXJVQWBWHUHXKWKXLWLX
      JXKWKUAXHXLWLUAXIABIJEFKLMNQRUICDIJGHKLOPQRUIUJUKULWTXCWOWPWRWSEKMQUMFLNR
      UMUNXAXBGKOQUMHLPRUMUNUOUPWIXEXGUQWMWIWQXFXDWCVMURTZVRURTZUAZWDURTZWQXFUD
      WHVQXMWBXNVNXMVPVMEMUSUTVSXNWAVRGOUSUTUJWEXPWGWDKQUSUTXOXPUAWQXFWDWDUEUCZ
      UEUCZXFVAUEUCZXFXMXNXPWQXRUDVMWDVRWDVBVCXPXRXSUDXOXPXQVAXFUEWDVDVEVFXOXSX
      FUDZXPXOXFURTXTVMVRVGXFVHUPUTVIVJVKUTVL $.
      $( [2-Apr-2000] $)


    $( Lemma for orthoarguesian law 5OA. $)
    5oalem4 $p |- ( ( ( ( ( x e. A /\ y e. B ) /\ ( z e. C /\ w e. D ) )
                    /\ ( f e. F /\ g e. G ) ) /\
                  ( ( x +h y ) = ( f +h g ) /\ ( z +h w ) = ( f +h g ) ) ) ->
                  ( x -h z ) e. ( ( ( A +H C ) i^i ( B +H D ) ) i^i
                    ( ( ( A +H F ) i^i ( B +H G ) ) +H
                      ( ( C +H F ) i^i ( D +H G ) ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin eqtr3 5oalem2 sylan2 adantlr
      5oalem3 elin sylanbrc ) ASZETBSZFTUACSZGTDSZHTUAUAZISZKTJSZLTUAZUAUOUPUBU
      CZUTVAUBUCZUDUQURUBUCZVDUDUAZUAUOUQUEUCZEGUFUCFHUFUCUGZTZVGEKUFUCFLUFUCUG
      GKUFUCHLUFUCUGUFUCZTVGVHVJUGTUSVFVIVBVFUSVCVEUDVIVCVEVDUHABCDEFGHMNOPUIUJ
      UKABCDEFGHIJKLMNOPQRULVGVHVJUMUN $.
      $( [2-Apr-2000] $)
  $}

  ${
    5oalem5.1 $e |- A e. SH $.
    5oalem5.2 $e |- B e. SH $.
    5oalem5.3 $e |- C e. SH $.
    5oalem5.4 $e |- D e. SH $.
    5oalem5.5 $e |- F e. SH $.
    5oalem5.6 $e |- G e. SH $.
    5oalem5.7 $e |- R e. SH $.
    5oalem5.8 $e |- S e. SH $.
    $( Lemma for orthoarguesian law 5OA. $)
    5oalem5 $p |- ( ( ( ( ( x e. A /\ y e. B ) /\ ( z e. C /\ w e. D ) )
                    /\ ( ( f e. F /\ g e. G ) /\ ( v e. R /\ u e. S ) ) ) /\
             ( ( ( x +h y ) = ( v +h u ) /\ ( z +h w ) = ( v +h u ) ) /\
                 ( f +h g ) = ( v +h u ) ) ) ->
                  ( x -h z ) e.
                 ( ( ( ( A +H C ) i^i ( B +H D ) ) i^i
                     ( ( ( A +H R ) i^i ( B +H S ) ) +H
                       ( ( C +H R ) i^i ( D +H S ) ) ) )
                i^i ( ( ( ( A +H F ) i^i ( B +H G ) ) i^i
                        ( ( ( A +H R ) i^i ( B +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) )
                   +H ( ( ( C +H F ) i^i ( D +H G ) ) i^i
                        ( ( ( C +H R ) i^i ( D +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin simpr anim2i simpl 5oalem4 syl2an
      chil sheli adantr anim12i c0v hvsubsub4 anandirs hvsubid opreq2d adantl
      hvsubcl hvsub0 syl 3eqtrd adantrr anandir sylib simprr jca anim1i an4s
      sylanb shscli shincli shsvsi 3syl eqeltrrd elin sylanbrc ) AUEZGUFZBUEZHU
      FZUGZCUEZIUFZDUEZJUFZUGZUGZMUEZOUFZNUEZPUFZUGZEUEZKUFFUEZLUFUGZUGZUGZWHWJ
      UHUIXDXEUHUIZUJZWMWOUHUIXIUJZUGZWSXAUHUIXIUJZUGZUGZWHWMUKUIZGIULUIHJULUIU
      MGKULUIZHLULUIZUMZIKULUIZJLULUIZUMZULUIUMZUFZXPGOULUIZHPULUIZUMZXSOKULUIZ
      PLULUIZUMZULUIZUMZIOULUIZJPULUIZUMZYBYJULUIZUMZULUIZUFXPYCYRUMUFXHWRXFUGX
      LYDXNXGXFWRXCXFUNUOXLXMUPABCDGHIJEFKLQRSTUCUDUQURXOWHWSUKUIZWMWSUKUIZUKUI
      ZXPYRXHUUAXPUJZXNWRXCUUBXFWRWHUSUFZWMUSUFZUGZWSUSUFZUUBXCWLUUCWQUUDWIUUCW
      KWHGQUTVAWNUUDWPWMISUTVAVBWTUUFXBWSOUAUTVAUUEUUFUGUUAXPWSWSUKUIZUKUIZXPVC
      UKUIZXPUUCUUDUUFUUAUUHUJWHWSWMWSVDVEUUFUUHUUIUJUUEUUFUUGVCXPUKWSVFVGVHUUE
      UUIXPUJZUUFUUEXPUSUFUUJWHWMVIXPVJVKVAVLURVMVAXOWLXCUGZWQXCUGZUGZXFUGZXJXM
      UGZXKXMUGZUGZUGYSYLUFZYTYQUFZUGZUUAYRUFXHUUNXNUUQXHUUMXFXHWRXCUGUUMXGXCWR
      XCXFUPUOWLWQXCVNVOWRXCXFVPVQXNUUOUUPXLXJXMXJXKUPVRXLXKXMXJXKUNVRVQVBUUNUU
      KXFUGZUULXFUGZUGUUQUUTUUKUULXFVNUVAUUOUVBUUPUUTUVAUUOUGUURUVBUUPUGUUSABMN
      GHOPEFKLQRUAUBUCUDUQCDMNIJOPEFKLSTUAUBUCUDUQVBVSVTYLYQYSYTYGYKYEYFGOQUAWA
      HPRUBWAWBXSYJXQXRGKQUCWAHLRUDWAWBYHYIOKUAUCWAPLUBUDWAWBZWAWBYOYPYMYNIOSUA
      WAJPTUBWAWBYBYJXTYAIKSUCWAJLTUDWAWBUVCWAWBWCWDWEXPYCYRWFWG $.
      $( [2-May-2000] $)

    $( Lemma for orthoarguesian law 5OA. $)
    5oalem6 $p |- ( ( ( ( ( x e. A /\ y e. B ) /\ h = ( x +h y ) ) /\
                        ( ( z e. C /\ w e. D ) /\ h = ( z +h w ) ) ) /\
                      ( ( ( f e. F /\ g e. G ) /\ h = ( f +h g ) ) /\
                        ( ( v e. R /\ u e. S ) /\ h = ( v +h u ) ) ) ) ->
           h e. ( B +H ( A i^i ( C +H
                 ( ( ( ( A +H C ) i^i ( B +H D ) ) i^i
                     ( ( ( A +H R ) i^i ( B +H S ) ) +H
                       ( ( C +H R ) i^i ( D +H S ) ) ) )
                i^i ( ( ( ( A +H F ) i^i ( B +H G ) ) i^i
                        ( ( ( A +H R ) i^i ( B +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) )
                   +H ( ( ( C +H F ) i^i ( D +H G ) ) i^i
                        ( ( ( C +H R ) i^i ( D +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) ) ) ) ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin an4 eqeq1 biimpcd anim12d exp3a
      com3l imp32 anim2i an4s syl2anb 5oalem5 syl wi shscli shincli 5oalem1
      expr adantrr adantr mpd ) AUFZGUGBUFZHUGUHZOUFZVOVPUIUJZUKZUHZCUFZIUGZDUF
      ZJUGZUHZVRWBWDUIUJZUKZUHUHZMUFZPUGNUFZQUGUHZVRWJWKUIUJZUKZUHEUFZKUGFUFZLU
      GUHZVRWOWPUIUJZUKZUHUHZUHZVOWBULUJGIUMUJZHJUMUJZUNZGKUMUJZHLUMUJZUNZIKUMU
      JZJLUMUJZUNZUMUJZUNZGPUMUJZHQUMUJZUNZXGPKUMUJZQLUMUJZUNZUMUJZUNZIPUMUJZJQ
      UMUJZUNZXJXRUMUJZUNZUMUJZUNZUGZVRHGIYGUMUJUNUMUJUGZXAVQWFUHZWLWQUHZUHZVSW
      RUKZWGWRUKZUHZWMWRUKZUHZUHZYHWIYJVTWHUHZUHYKWNWSUHZUHYRWTVQVTWFWHUOWLWNWQ
      WSUOYJYKYSYTYRYSYTUHYQYLYSWNWSYQWSYSWNYQWSYSWNYQWSYSYOWNYPWSVTYMWHYNVTWSY
      MVRVSWRUPUQWHWSYNVRWGWRUPUQURWNWSYPVRWMWRUPUQURUSUTVAVBVCVDABCDEFGHIJKLMN
      PQRSTUAUBUCUDUEVEVFWIYHYIVGZWTWAWFUUAWHWAWCUUAWEWAWCYHYIABCOGHIYGRSTXLYFX
      DXKXBXCGIRTVHHJSUAVHVIXGXJXEXFGKRUDVHHLSUEVHVIZXHXIIKTUDVHJLUAUEVHVIZVHVI
      XTYEXOXSXMXNGPRUBVHHQSUCVHVIXGXRUUBXPXQPKUBUDVHQLUCUEVHVIZVHVIYCYDYAYBIPT
      UBVHJQUAUCVHVIXJXRUUCUUDVHVIVHVIVJVKVLVLVMVN $.
      $( [4-May-2000] $)

    ${
      $d A h f g u v w x y z $.  $d B h f g u v w x y z $.
      $d C h f g u v w x y z $.  $d D h f g u v w x y z $.
      $d F h f g u v w x y z $.  $d G h f g u v w x y z $.
      $d R h f g u v w x y z $.  $d S h f g u v w x y z $.
      $( Lemma for orthoarguesian law 5OA. $)
      5oalem7 $p |- ( ( ( A +H B ) i^i ( C +H D ) ) i^i
              ( ( F +H G ) i^i ( R +H S ) ) ) C_ ( B +H ( A i^i ( C +H
                 ( ( ( ( A +H C ) i^i ( B +H D ) ) i^i
                     ( ( ( A +H R ) i^i ( B +H S ) ) +H
                       ( ( C +H R ) i^i ( D +H S ) ) ) )
                i^i ( ( ( ( A +H F ) i^i ( B +H G ) ) i^i
                        ( ( ( A +H R ) i^i ( B +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) )
                   +H ( ( ( C +H F ) i^i ( D +H G ) ) i^i
                        ( ( ( C +H R ) i^i ( D +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) ) ) ) ) ) ) $=
        ( vh vx vy vz vw vf vg vv vu cph co cin cv wcel wa cva wceq wex ee4anv
        exrot4 2exbii bitri elin wrex shseli r2ex anbi12i 3bitr4ri bitr4i
        5oalem6 19.23aivv sylbi ssriv ) QABUFUGZCDUFUGZUHZGHUFUGZEFUFUGZUHZUHZB
        ACACUFUGBDUFUGUHAEUFUGBFUFUGUHZCEUFUGDFUFUGUHZUFUGUHAGUFUGBHUFUGUHVQGEU
        FUGHFUFUGUHZUFUGUHCGUFUGDHUFUGUHVRVSUFUGUHUFUGUHUFUGUHUFUGZQUIZVPUJZRUI
        ZAUJSUIZBUJUKWAWCWDULUGUMZUKZTUIZCUJUAUIZDUJUKWAWGWHULUGUMZUKZUKZUBUIZG
        UJUCUIZHUJUKWAWLWMULUGUMZUKZUDUIZEUJUEUIZFUJUKWAWPWQULUGUMZUKZUKZUKZUEU
        NUDUNZUCUNUBUNZUAUNTUNZSUNRUNZWAVTUJZWKUAUNTUNZWTUEUNUDUNZUKZUCUNUBUNZS
        UNRUNXGSUNRUNZXHUCUNUBUNZUKZXEWBXGXHRSUBUCUOXDXJRSXDXBUAUNTUNZUCUNUBUNX
        JXBTUAUBUCUPXNXIUBUCWKWTTUAUDUEUOUQURUQWBWAVLUJZWAVOUJZUKXMWAVLVOUSXKXO
        XLXPWAVJUJZWAVKUJZUKWFSUNRUNZWJUAUNTUNZUKXOXKXQXSXRXTXQWESBUTRAUTXSRSAB
        WAIJVAWERSABVBURXRWIUADUTTCUTXTTUACDWAKLVAWITUACDVBURVCWAVJVKUSWFWJRSTU
        AUOVDWAVMUJZWAVNUJZUKWOUCUNUBUNZWSUEUNUDUNZUKXPXLYAYCYBYDYAWNUCHUTUBGUT
        YCUBUCGHWAMNVAWNUBUCGHVBURYBWRUEFUTUDEUTYDUDUEEFWAOPVAWRUDUEEFVBURVCWAV
        MVNUSWOWSUBUCUDUEUOVDVCVEVDXDXFRSXCXFTUAXBXFUBUCXAXFUDUERSTUAUDUEABCDEF
        UBUCQGHIJKLMNOPVFVGVGVGVGVHVI $.
        $( [4-May-2000] $)
    $}
  $}

  ${
    5oa.1 $e |- A e. CH $.
    5oa.2 $e |- B e. CH $.
    5oa.3 $e |- C e. CH $.
    5oa.4 $e |- D e. CH $.
    5oa.5 $e |- F e. CH $.
    5oa.6 $e |- G e. CH $.
    5oa.7 $e |- R e. CH $.
    5oa.8 $e |- S e. CH $.
    5oa.9 $e |- A C_ ( _|_ ` B ) $.
    5oa.10 $e |- C C_ ( _|_ ` D ) $.
    5oa.11 $e |- F C_ ( _|_ ` G ) $.
    5oa.12 $e |- R C_ ( _|_ ` S ) $.
    $( Orthoarguesian law 5OA. This 8-variable inference is called 5OA because
       it can be converted to a 5-variable equation (see Quantum Logic
       Explorer). $)
    5oai $p |- ( ( ( A vH B ) i^i ( C vH D ) ) i^i
            ( ( F vH G ) i^i ( R vH S ) ) ) C_ ( B vH ( A i^i ( C vH
               ( ( ( ( A vH C ) i^i ( B vH D ) ) i^i
                   ( ( ( A vH R ) i^i ( B vH S ) ) vH
                     ( ( C vH R ) i^i ( D vH S ) ) ) )
              i^i ( ( ( ( A vH F ) i^i ( B vH G ) ) i^i
                      ( ( ( A vH R ) i^i ( B vH S ) ) vH
                        ( ( F vH R ) i^i ( G vH S ) ) ) )
                 vH ( ( ( C vH F ) i^i ( D vH G ) ) i^i
                      ( ( ( C vH R ) i^i ( D vH S ) ) vH
                        ( ( F vH R ) i^i ( G vH S ) ) ) ) ) ) ) ) ) $=
      ( chj co cin cph cort cfv wss wceq osumi ax-mp ineq12i chshii 5oalem7
      eqsstr3i shscli shincli shsleji chsleji ss2in mp2an shjshcli shlej2i
      shlej1i sstri chjcli chincli shjcli sslin ) ABUAUBZCDUAUBZUCZGHUAUBZEFUAU
      BZUCZUCZBACACUDUBZBDUDUBZUCZAEUDUBZBFUDUBZUCZCEUDUBZDFUDUBZUCZUDUBZUCZAGU
      DUBZBHUDUBZUCZWAGEUDUBZHFUDUBZUCZUDUBZUCZCGUDUBZDHUDUBZUCZWDWLUDUBZUCZUDU
      BZUCZUDUBZUCZUDUBZBACACUAUBZBDUAUBZUCZAEUAUBZBFUAUBZUCZCEUAUBZDFUAUBZUCZU
      AUBZUCZAGUAUBZBHUAUBZUCZXJGEUAUBZHFUAUBZUCZUAUBZUCZCGUAUBZDHUAUBZUCZXMYAU
      AUBZUCZUAUBZUCZUAUBZUCZUAUBZVOABUDUBZCDUDUBZUCZGHUDUBZEFUDUBZUCZUCXDYPVKY
      SVNYNVIYOVJABUEUFUGYNVIUHQABIJUIUJCDUEUFUGYOVJUHRCDKLUIUJUKYQVLYRVMGHUEUF
      UGYQVLUHSGHMNUIUJEFUEUFUGYRVMUHTEFOPUIUJUKUKABCDEFGHAIULZBJULZCKULZDLULZG
      MULZHNULZEOULZFPULZUMUNXDBXCUAUBZYMBXCUUAAXBYTCXAUUBWFWTVRWEVPVQACYTUUBUO
      BDUUAUUCUOUPWAWDVSVTAEYTUUFUOBFUUAUUGUOUPZWBWCCEUUBUUFUODFUUCUUGUOUPZUOUP
      WNWSWIWMWGWHAGYTUUDUOBHUUAUUEUOUPWAWLUUIWJWKGEUUDUUFUOHFUUEUUGUOUPZUOUPZW
      QWRWOWPCGUUBUUDUODHUUCUUEUOUPWDWLUUJUUKUOUPZUOUPZUOUPZUQXCYLUGZUUHYMUGXBY
      KUGUUPXBCXAUAUBZYKCXAUUBUUNUQXAYJUGZUUQYKUGWFXOUGZWTYIUGUURVRXGUGZWEXNUGU
      USVPXEUGVQXFUGUUTACIKURBDJLURVPXEVQXFUSUTWEWAWDUAUBZXNWAWDUUIUUJUQUVAWAXM
      UAUBZXNWDXMUGZUVAUVBUGWBXKUGWCXLUGUVCCEKOURDFLPURWBXKWCXLUSUTZWDXMWAUUJXK
      XLCEUUBUUFVADFUUCUUGVAUPZUUIVBUJWAXJUGZUVBXNUGVSXHUGVTXIUGUVFAEIOURBFJPUR
      VSXHVTXIUSUTZWAXJXMUUIXHXIAEYTUUFVABFUUAUUGVAUPZUVEVCUJVDVDVRXGWEXNUSUTWT
      WNWSUAUBZYIWNWSUULUUMUQUVIWNYHUAUBZYIWSYHUGZUVIUVJUGWQYFUGZWRYGUGUVKWOYDU
      GWPYEUGUVLCGKMURDHLNURWOYDWPYEUSUTWRWDWLUAUBZYGWDWLUUJUUKUQUVMWDYAUAUBZYG
      WLYAUGZUVMUVNUGWJXSUGWKXTUGUVOGEMOURHFNPURWJXSWKXTUSUTZWLYAWDUUKXSXTGEUUD
      UUFVAHFUUEUUGVAUPZUUJVBUJUVCUVNYGUGUVDWDXMYAUUJUVEUVQVCUJVDVDWQYFWRYGUSUT
      WSYHWNUUMYFYGYFYDYECGKMVEDHLNVEVFULXMYAUVEUVQVAUPZUULVBUJWNYCUGZUVJYIUGWI
      XRUGZWMYBUGUVSWGXPUGWHXQUGUVTAGIMURBHJNURWGXPWHXQUSUTWMWAWLUAUBZYBWAWLUUI
      UUKUQUWAWAYAUAUBZYBUVOUWAUWBUGUVPWLYAWAUUKUVQUUIVBUJUVFUWBYBUGUVGWAXJYAUU
      IUVHUVQVCUJVDVDWIXRWMYBUSUTWNYCYHUULXRYBXRXPXQAGIMVEBHJNVEVFULXJYAUVHUVQV
      AUPZUVRVCUJVDVDWFXOWTYIUSUTXAYJCUUNXOYIXOXGXNXEXFACIKVEBDJLVEVFXJXMUVHUVE
      VGVFULYCYHUWCUVRVAUPZUUBVBUJVDXBYKAVHUJXCYLBUUOAYKYTCYJUUBUWDVAUPUUAVBUJV
      DVD $.
      $( [5-May-2000] $)
  $}

  ${
    $d x y z w v B $.  $d x y z w v C $.  $d x y z w v R $.  $d x y z w v S $.
    3oalem1.1 $e |- B e. CH $.
    3oalem1.2 $e |- C e. CH $.
    3oalem1.3 $e |- R e. CH $.
    3oalem1.4 $e |- S e. CH $.
    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem1 $p |- ( ( ( ( x e. B /\ y e. R ) /\ v = ( x +h y ) ) /\
                  ( ( z e. C /\ w e. S ) /\ v = ( z +h w ) ) ) ->
                 ( ( ( x e. ~H /\ y e. ~H ) /\ v e. ~H ) /\
                  ( z e. ~H /\ w e. ~H ) ) ) $=
      ( cv wcel wa cva co wceq chil cheli anim12i hvaddcl eleq1 syl5ibrcom
      imdistani sylan adantr ) ANZFOZBNZHOZPZENZUIUKQRZSZPUITOZUKTOZPZUNTOZPZCN
      ZGOZDNZIOZPZUNVBVDQRSZPVBTOZVDTOZPZUMUSUPVAUJUQULURUIFJUAUKHLUAUBUSUPUTUS
      UTUPUOTOUIUKUCUNUOTUDUEUFUGVFVJVGVCVHVEVIVBGKUAVDIMUAUBUHUB $.
      $( [19-Oct-1999] $)

    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem2 $p |- ( ( ( ( x e. B /\ y e. R ) /\ v = ( x +h y ) ) /\
                  ( ( z e. C /\ w e. S ) /\ v = ( z +h w ) ) ) ->
            v e. ( B +H ( R i^i ( S +H ( ( B +H C ) i^i ( R +H S ) ) ) ) ) ) $=
      ( cv wcel wa cva co wceq cph cin simplll simpllr cmv chil 3oalem1
      hvaddsub12 3anidm23 c0v hvsubid opreq2d ax-hvaddid sylan9eqr eqtr3d
      ad2ant2l adantlr syl simprlr eqtr2 opreq1d simpl anim1i hvsub4 syldan
      ad2antrr hvsubcl hvaddid2 adantll 3eqtrd ad2ant2rl simpr anim2i syl11anc
      ad2antll ancoms adantrr 3eqtr3d simpll chshii shsvsi ancom shscomi
      eleq2i 3imtr4i syl2an eqeltrd simplr elin sylanbrc shscli shincli shsvai
      eqeltrrd wb eleq1 ad2antlr mpbird ) ANZFOZBNZHOZPZENZWRWTQRZSZPZCNZGOZDNZ
      IOZPZXCXGXIQRZSZPZPZXCFHIFGTRZHITRZUAZTRZUAZTRZOZXDYAOZXOWSWTXTOZYCWSXAXE
      XNUBXOXAWTXSOYDWSXAXEXNUCXOXIWTXIUDRZQRZWTXSXOWRUEOZWTUEOZPZXCUEOZPXGUEOZ
      XIUEOZPZPZYFWTSZABCDEFGHIJKLMUFZYIYMYOYJYHYLYOYGYKYHYLPZWTXIXIUDRZQRZYFWT
      YHYLYSYFSWTXIXIUGUHYLYHYSWTUIQRWTYLYRUIWTQXIUJZUKWTULUMUNUOUPUQXOXJYEXROZ
      YFXSOXFXHXJXMURXOYEXPOYEXQOZUUAXOYEXGWRUDRZXPXOXDWRXIQRZUDRZXLUUDUDRZYEUU
      CXEXMUUEUUFSXBXKXEXMPXDXLUUDUDXCXDXLUSUTUOXOYNUUEYESZYPYIYLUUGYJYKYIYLPZU
      UEWRWRUDRZYEQRZUIYEQRZYEYIYLYGYLPZUUEUUJSYIYGYLYGYHVAVBWRWTWRXIVCVDUUHUUI
      UIYEQYGUUIUISYHYLWRUJVEUTYHYLUUKYESZYGYQYEUEOUUMWTXIVFYEVGUQVHVIVJUQXOYNU
      UFUUCSZYPYIYMUUNYJYGYMUUNYHYGYMPZUUFUUCYRQRZUUCUIQRZUUCUUOYMUULUUFUUPSYGY
      MVKYMYLYGYKYLVKVLXGXIWRXIVCVMUUOYRUIUUCQYLYRUISYGYKYTVNUKYGYKUUQUUCSZYLYK
      YGUURYKYGPUUCUEOUURXGWRVFUUCULUQVOVPVIUPUPUQVQXFWSXHUUCXPOZXNWSXAXEVRXHXJ
      XMVRXHWSPUUCGFTRZOWSXHPUUSGFXGWRGKVSZFJVSZVTWSXHWAXPUUTUUCFGUVBUVAWBWCWDW
      EWFXFXAXJUUBXNWSXAXEWGXHXJXMWGHIWTXIHLVSZIMVSZVTWEYEXPXQWHWIIXRXIYEUVDXPX
      QFGUVBUVAWJHIUVCUVDWJWKZWLVMWMWTHXSWHWIFXTWRWTUVBHXSUVCIXRUVDUVEWJWKWLVMX
      EYBYCWNXBXNXCXDYAWOWPWQ $.
      $( [19-Oct-1999] $)

    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem3 $p |- ( ( B +H R ) i^i ( C +H S ) ) C_
                   ( B +H ( R i^i ( S +H ( ( B +H C ) i^i ( R +H S ) ) ) ) ) $=
      ( vv vx vy vz vw cph co cin cv wcel wa cva wceq wex wrex chseli r2ex
      bitri anbi12i elin ee4anv 3bitr4i 3oalem2 19.23aivv sylbi ssriv ) IACNOZB
      DNOZPZACDABNOCDNOPNOPNOZIQZUQRZJQZARKQZCRSUSVAVBTOUAZSZLQZBRMQZDRSUSVEVFT
      OUAZSZSZMUBLUBZKUBJUBZUSURRZUSUORZUSUPRZSVDKUBJUBZVHMUBLUBZSUTVKVMVOVNVPV
      MVCKCUCJAUCVOJKACUSEGUDVCJKACUEUFVNVGMDUCLBUCVPLMBDUSFHUDVGLMBDUEUFUGUSUO
      UPUHVDVHJKLMUIUJVJVLJKVIVLLMJKLMIABCDEFGHUKULULUMUN $.
      $( [19-Oct-1999] $)
  $}

  ${
    3oalem4.3 $e |- R = ( ( _|_ ` B ) i^i ( B vH A ) ) $.
    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem4 $p |- R C_ ( _|_ ` B ) $=
      ( cort cfv chj co cin inss1 eqsstri ) CBEFZBAGHZILDLMJK $.
      $( [19-Oct-1999] $)
  $}

  ${
    3oa.1 $e |- A e. CH $.
    3oa.2 $e |- B e. CH $.
    3oa.3 $e |- C e. CH $.
    3oa.4 $e |- R = ( ( _|_ ` B ) i^i ( B vH A ) ) $.
    3oa.5 $e |- S = ( ( _|_ ` C ) i^i ( C vH A ) ) $.
    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem5 $p |- ( ( B +H R ) i^i ( C +H S ) ) =
                  ( ( B vH R ) i^i ( C vH S ) ) $=
      ( cph co chj cort cfv wss wceq 3oalem4 cin cch choccli chjcli chincli
      eqeltri osumi ax-mp chshii shscomi chjcomi 3eqtr4i ineq12i ) BDKLZBDMLZCE
      KLZCEMLZDBKLZDBMLZULUMDBNOZPUPUQQABDIRDBDURBAMLZSTIURUSBGUABAGFUBUCUDZGUE
      UFBDBGUGDUTUGUHBDGUTUIUJECKLZECMLZUNUOECNOZPVAVBQACEJRECEVCCAMLZSTJVCVDCH
      UACAHFUBUCUDZHUEUFCECHUGEVEUGUHCEHVEUIUJUK $.
      $( [19-Oct-1999] $)

    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem6 $p |- ( B +H ( R i^i ( S +H ( ( B +H C ) i^i ( R +H S ) ) ) ) ) C_
                  ( B vH ( R i^i ( S vH ( ( B vH C ) i^i ( R vH S ) ) ) ) ) $=
      ( cph co cin chj chshii cort cfv cch choccli chjcli chincli eqeltri
      shscli shincli shsleji wss chsleji ssrin ax-mp sslin sstri shlej2i ) BDEB
      CKLZDEKLZMZKLZMZKLBUQNLZBDEBCNLZDENLZMZNLZMZNLZBUQBGOZDUPDDBPQZBANLZMRIVF
      VGBGSBAGFTUAUBZOZEUOEECPQZCANLZMRJVJVKCHSCAHFTUAUBZOZUMUNBCVECHOUCDEVIVMU
      CUDZUCUDZUEUQVCUFZURVDUFUPVBUFVPUPEUONLZVBEUOVMVNUEUOVAUFVQVBUFUOUSUNMZVA
      UMUSUFUOVRUFBCGHUGUMUSUNUHUIUNUTUFVRVAUFDEVHVLUGUNUTUSUJUIUKUOVAEVNVAUSUT
      BCGHTDEVHVLTUAZOVMULUIUKUPVBDUJUIUQVCBVOVCDVBVHEVAVLVSTUAOVEULUIUK $.
      $( [19-Oct-1999] $)

    $( 3OA (weak) orthoarguesian law.  Equation IV of [GodowskiGreechie]
       p. 249. $)
    3oai $p |- ( ( B vH R ) i^i ( C vH S ) ) C_
                  ( B vH ( R i^i ( S vH ( ( B vH C ) i^i ( R vH S ) ) ) ) ) $=
      ( chj co cin cph 3oalem5 cort cfv cch choccli chjcli chincli eqeltri
      3oalem3 3oalem6 sstri eqsstr3i ) BDKLCEKLMBDNLCENLMZBDEBCKLDEKLMKLMKLZABC
      DEFGHIJOUGBDEBCNLDENLMNLMNLUHBCDEGHDBPQZBAKLZMRIUIUJBGSBAGFTUAUBECPQZCAKL
      ZMRJUKULCHSCAHFTUAUBUCABCDEFGHIJUDUEUF $.
      $( [19-Oct-1999] $)
  $}

