$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Orthoarguesian laws 5OA and 3OA
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    5oalem1.1 $e |- A e. SH $.
    5oalem1.2 $e |- B e. SH $.
    5oalem1.3 $e |- C e. SH $.
    5oalem1.4 $e |- R e. SH $.
    $( Lemma for orthoarguesian law 5OA. $)
    5oalem1 $p |- ( ( ( ( x e. A /\ y e. B ) /\ v = ( x +h y ) ) /\
                  ( z e. C /\ ( x -h z ) e. R ) ) ->
                  v e. ( B +H ( A i^i ( C +H R ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin shscl shincl shsva shscom syl6eleq
      pm3.26 ad2antrr chil hvaddsub12t 3anidm23 c0v hvsubidt opreq2d
      ax-hvaddid sylan9eqr eqtr3d shel adantr syl2an adantl eqeltrrd jca elin
      sylibr pm3.27 sylanc wb eleq1 ad2antlr mpbird ) AMZENZBMZFNZOZDMZVLVNPQZR
      ZOZCMZGNZVLWASQZHNZOZOZVQFEGHTQZUAZTQZNZVRWINZVLWHNZVOWKWFWLVOOVRWHFTQWIW
      HFVLVNEWGIGHKLUBUCZJUDWHFWMJUEUFWFVMVLWGNZOWLWFVMWNVPVMVSWEVMVOUGUHWFWAWC
      PQZVLWGVLUINZWAUINZWOVLRVTWEWPWQOVLWAWASQZPQZWOVLWPWQWSWORVLWAWAUJUKWQWPW
      SVLULPQVLWQWRULVLPWAUMUNVLUOUPUQVMWPVOVSVLEIURUHWBWQWDWAGKURUSUTWEWOWGNVT
      GHWAWCKLUDVAVBVCVLEWGVDVEVPVOVSWEVMVOVFUHVGVSWJWKVHVPWEVQVRWIVIVJVK $.
      $( [1-Apr-2000] $)
  $}

  ${
    5oalem2.1 $e |- A e. SH $.
    5oalem2.2 $e |- B e. SH $.
    5oalem2.3 $e |- C e. SH $.
    5oalem2.4 $e |- D e. SH $.
    $( Lemma for orthoarguesian law 5OA. $)
    5oalem2 $p |- ( ( ( ( x e. A /\ y e. B ) /\ ( z e. C /\ w e. D ) ) /\
                  ( x +h y ) = ( z +h w ) ) ->
                  ( x -h z ) e. ( ( A +H C ) i^i ( B +H D ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin shsvs ad2ant2r adantr ancom shscom
      eleq2i 3imtr4 ad2ant2l chil wb opreq1 adantl c0v pm3.27 anim2i ancoms
      hvsub4t syldan hvsubidt opreq2d ad2antlr hvsubclt ax-hvaddid syl adantlr
      3eqtrd adantrr pm3.26 anim1i sylanc opreq1d ad2antrl hvaddid2t adantrl
      adantll 3eqtr3d eleq1d shel anim12i sylan mpbird jca elin sylibr ) AMZENZ
      BMZFNZOZCMZGNZDMZHNZOZOZWFWHPQZWKWMPQZRZOZWFWKSQZEGTQZNZXAFHTQZNZOXAXBXDU
      ANWTXCXEWPXCWSWGWLXCWIWNEGWFWKIKUBUCUDWTXEWMWHSQZXDNZWPXGWSWIWNXGWGWLWNWI
      OXFHFTQZNWIWNOXGHFWMWHLJUBWIWNUEXDXHXFFHJLUFUGUHUIUDWFUJNZWHUJNZOZWKUJNZW
      MUJNZOZOZWSXEXGUKWPXOWSOZXAXFXDXPWQWKWHPQZSQZWRXQSQZXAXFWSXRXSRXOWQWRXQSU
      LUMXOXRXARZWSXKXLXTXMXKXLOXRXAWHWHSQZPQZXAUNPQZXAXKXLXLXJOZXRYBRXLXKYDXKX
      JXLXIXJUOUPUQWFWHWKWHURUSXJYBYCRXIXLXJYAUNXAPWHUTVAVBXIXLYCXARZXJXIXLOXAU
      JNYEWFWKVCXAVDVEVFVGVHUDXOXSXFRZWSXJXNYFXIXJXNOZXSWKWKSQZXFPQZUNXFPQZXFXN
      YDXSYIRYGWKWMWKWHURXJXNUOXNXJYDXNXLXJXLXMVIVJUQVKXLYIYJRXJXMXLYHUNXFPWKUT
      VLVMXJXMYJXFRZXLXMXJYKXMXJOXFUJNYKWMWHVCXFVNVEUQVOVGVPUDVQVRWJXKWOXNWGXIW
      IXJWFEIVSWHFJVSVTWLXLWNXMWKGKVSWMHLVSVTVTWAWBWCXAXBXDWDWE $.
      $( [2-Apr-2000] $)
  $}

  ${
    5oalem3.1 $e |- A e. SH $.
    5oalem3.2 $e |- B e. SH $.
    5oalem3.3 $e |- C e. SH $.
    5oalem3.4 $e |- D e. SH $.
    5oalem3.5 $e |- F e. SH $.
    5oalem3.6 $e |- G e. SH $.
    $( Lemma for orthoarguesian law 5OA. $)
    5oalem3 $p |- ( ( ( ( ( x e. A /\ y e. B ) /\ ( z e. C /\ w e. D ) )
                    /\ ( f e. F /\ g e. G ) ) /\
                  ( ( x +h y ) = ( f +h g ) /\ ( z +h w ) = ( f +h g ) ) ) ->
                  ( x -h z ) e. ( ( ( A +H F ) i^i ( B +H G ) ) +H
                    ( ( C +H F ) i^i ( D +H G ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin 5oalem2 anim12i an4s anandir sylanb
      shscl shincl shsvs syl wb chil c0v hvsubsub4t anandirs hvsubidt opreq2d
      adantl hvsubclt hvsub0t adantr 3eqtrd shel syl2an eleq1d mpbid ) ASZETZBS
      ZFTZUAZCSZGTZDSZHTZUAZUAZISZKTZJSZLTZUAZUAZVMVOUBUCWDWFUBUCZUDZVRVTUBUCWJ
      UDZUAZUAZVMWDUEUCZVRWDUEUCZUEUCZEKUFUCZFLUFUCZUGZGKUFUCZHLUFUCZUGZUFUCZTZ
      VMVRUEUCZXDTZWNWOWTTZWPXCTZUAZXEVQWHUAZWBWHUAZUAWMXJWIXKWKXLWLXJXKWKUAXHX
      LWLUAXIABIJEFKLMNQRUHCDIJGHKLOPQRUHUIUJVQWBWHUKULWTXCWOWPWRWSEKMQUMFLNRUM
      UNXAXBGKOQUMHLPRUMUNUOUPWIXEXGUQWMWIWQXFXDVMURTZVRURTZUAZWDURTZWQXFUDWCWH
      XOXPUAWQXFWDWDUEUCZUEUCZXFUSUEUCZXFXMXNXPWQXRUDVMWDVRWDUTVAXPXRXSUDXOXPXQ
      USXFUEWDVBVCVDXOXSXFUDZXPXOXFURTXTVMVRVEXFVFUPVGVHVQXMWBXNVNXMVPVMEMVIVGV
      SXNWAVRGOVIVGUIWEXPWGWDKQVIVGVJVKVGVL $.
      $( [2-Apr-2000] $)


    $( Lemma for orthoarguesian law 5OA. $)
    5oalem4 $p |- ( ( ( ( ( x e. A /\ y e. B ) /\ ( z e. C /\ w e. D ) )
                    /\ ( f e. F /\ g e. G ) ) /\
                  ( ( x +h y ) = ( f +h g ) /\ ( z +h w ) = ( f +h g ) ) ) ->
                  ( x -h z ) e. ( ( ( A +H C ) i^i ( B +H D ) ) i^i
                    ( ( ( A +H F ) i^i ( B +H G ) ) +H
                      ( ( C +H F ) i^i ( D +H G ) ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin 5oalem2 eqtr3t sylan2 adantlr
      5oalem3 jca elin sylibr ) ASZETBSZFTUACSZGTDSZHTUAUAZISZKTJSZLTUAZUAUPUQU
      BUCZVAVBUBUCZUDURUSUBUCZVEUDUAZUAZUPURUEUCZEGUFUCFHUFUCUGZTZVIEKUFUCFLUFU
      CUGGKUFUCHLUFUCUGUFUCZTZUAVIVJVLUGTVHVKVMUTVGVKVCUTVDVFUDVKVGABCDEFGHMNOP
      UHVDVFVEUIUJUKABCDEFGHIJKLMNOPQRULUMVIVJVLUNUO $.
      $( [2-Apr-2000] $)
  $}

  ${
    5oalem5.1 $e |- A e. SH $.
    5oalem5.2 $e |- B e. SH $.
    5oalem5.3 $e |- C e. SH $.
    5oalem5.4 $e |- D e. SH $.
    5oalem5.5 $e |- F e. SH $.
    5oalem5.6 $e |- G e. SH $.
    5oalem5.7 $e |- R e. SH $.
    5oalem5.8 $e |- S e. SH $.
    $( Lemma for orthoarguesian law 5OA. $)
    5oalem5 $p |- ( ( ( ( ( x e. A /\ y e. B ) /\ ( z e. C /\ w e. D ) )
                    /\ ( ( f e. F /\ g e. G ) /\ ( v e. R /\ u e. S ) ) ) /\
             ( ( ( x +h y ) = ( v +h u ) /\ ( z +h w ) = ( v +h u ) ) /\
                 ( f +h g ) = ( v +h u ) ) ) ->
                  ( x -h z ) e.
                 ( ( ( ( A +H C ) i^i ( B +H D ) ) i^i
                     ( ( ( A +H R ) i^i ( B +H S ) ) +H
                       ( ( C +H R ) i^i ( D +H S ) ) ) )
                i^i ( ( ( ( A +H F ) i^i ( B +H G ) ) i^i
                        ( ( ( A +H R ) i^i ( B +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) )
                   +H ( ( ( C +H F ) i^i ( D +H G ) ) i^i
                        ( ( ( C +H R ) i^i ( D +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin 5oalem4 pm3.27 anim2i pm3.26 syl2an
      chil c0v hvsubsub4t anandirs hvsubidt opreq2d adantl hvsubclt hvsub0t
      syl adantr 3eqtrd shel anim12i adantrr anandir sylib simprr jca anim1i
      an4s sylanb shscl shincl shsvs 3syl eqeltrrd elin sylibr ) AUEZGUFZBUEZHU
      FZUGZCUEZIUFZDUEZJUFZUGZUGZMUEZOUFZNUEZPUFZUGZEUEZKUFFUEZLUFUGZUGZUGZWHWJ
      UHUIXDXEUHUIZUJZWMWOUHUIXIUJZUGZWSXAUHUIXIUJZUGZUGZWHWMUKUIZGIULUIHJULUIU
      MGKULUIZHLULUIZUMZIKULUIZJLULUIZUMZULUIUMZUFZXPGOULUIZHPULUIZUMZXSOKULUIZ
      PLULUIZUMZULUIZUMZIOULUIZJPULUIZUMZYBYJULUIZUMZULUIZUFZUGXPYCYRUMUFXOYDYS
      WRXFUGXLYDXHXNABCDGHIJEFKLQRSTUCUDUNXGXFWRXCXFUOUPXLXMUQURXOWHWSUKUIZWMWS
      UKUIZUKUIZXPYRXHUUBXPUJZXNWRXCUUCXFWHUSUFZWMUSUFZUGZWSUSUFZUUCWRXCUUFUUGU
      GUUBXPWSWSUKUIZUKUIZXPUTUKUIZXPUUDUUEUUGUUBUUIUJWHWSWMWSVAVBUUGUUIUUJUJUU
      FUUGUUHUTXPUKWSVCVDVEUUFUUJXPUJZUUGUUFXPUSUFUUKWHWMVFXPVGVHVIVJWLUUDWQUUE
      WIUUDWKWHGQVKVIWNUUEWPWMISVKVIVLWTUUGXBWSOUAVKVIURVMVIXOWLXCUGZWQXCUGZUGZ
      XFUGZXJXMUGZXKXMUGZUGZUGYTYLUFZUUAYQUFZUGZUUBYRUFXHUUOXNUURXHUUNXFXHWRXCU
      GUUNXGXCWRXCXFUQUPWLWQXCVNVOWRXCXFVPVQXNUUPUUQXLXJXMXJXKUQVRXLXKXMXJXKUOV
      RVQVLUULXFUGZUUMXFUGZUGUURUVAUUOUVBUUPUVCUUQUVAUVBUUPUGUUSUVCUUQUGUUTABMN
      GHOPEFKLQRUAUBUCUDUNCDMNIJOPEFKLSTUAUBUCUDUNVLVSUULUUMXFVNVTYLYQYTUUAYGYK
      YEYFGOQUAWAHPRUBWAWBXSYJXQXRGKQUCWAHLRUDWAWBYHYIOKUAUCWAPLUBUDWAWBZWAWBYO
      YPYMYNIOSUAWAJPTUBWAWBYBYJXTYAIKSUCWAJLTUDWAWBUVDWAWBWCWDWEVQXPYCYRWFWG
      $.
      $( [2-May-2000] $)

    $( Lemma for orthoarguesian law 5OA. $)
    5oalem6 $p |- ( ( ( ( ( x e. A /\ y e. B ) /\ h = ( x +h y ) ) /\
                        ( ( z e. C /\ w e. D ) /\ h = ( z +h w ) ) ) /\
                      ( ( ( f e. F /\ g e. G ) /\ h = ( f +h g ) ) /\
                        ( ( v e. R /\ u e. S ) /\ h = ( v +h u ) ) ) ) ->
           h e. ( B +H ( A i^i ( C +H
                 ( ( ( ( A +H C ) i^i ( B +H D ) ) i^i
                     ( ( ( A +H R ) i^i ( B +H S ) ) +H
                       ( ( C +H R ) i^i ( D +H S ) ) ) )
                i^i ( ( ( ( A +H F ) i^i ( B +H G ) ) i^i
                        ( ( ( A +H R ) i^i ( B +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) )
                   +H ( ( ( C +H F ) i^i ( D +H G ) ) i^i
                        ( ( ( C +H R ) i^i ( D +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) ) ) ) ) ) ) ) $=
      ( cv wcel wa cva co wceq cmv cph cin eqeq1 biimpcd anim12d exp3a com3l
      imp32 anim2i an4s an4 syl2anb 5oalem5 syl wi shscl shincl 5oalem1 exp32
      imp adantrr adantr mpd ) AUFZGUGBUFZHUGUHZOUFZVPVQUIUJZUKZUHZCUFZIUGZDUFZ
      JUGZUHZVSWCWEUIUJZUKZUHUHZMUFZPUGNUFZQUGUHZVSWKWLUIUJZUKZUHEUFZKUGFUFZLUG
      UHZVSWPWQUIUJZUKZUHUHZUHZVPWCULUJGIUMUJZHJUMUJZUNZGKUMUJZHLUMUJZUNZIKUMUJ
      ZJLUMUJZUNZUMUJZUNZGPUMUJZHQUMUJZUNZXHPKUMUJZQLUMUJZUNZUMUJZUNZIPUMUJZJQU
      MUJZUNZXKXSUMUJZUNZUMUJZUNZUGZVSHGIYHUMUJUNUMUJUGZXBVRWGUHZWMWRUHZUHZVTWS
      UKZWHWSUKZUHZWNWSUKZUHZUHZYIYKWAWIUHZUHYLWOWTUHZUHYSWJXAYKYLYTUUAYSYTUUAU
      HYRYMYTWOWTYRWTYTWOYRWTYTWOYRWTYTYPWOYQWTWAYNWIYOWAWTYNVSVTWSUOUPWIWTYOVS
      WHWSUOUPUQWOWTYQVSWNWSUOUPUQURUSUTVAVBVRWAWGWIVCWMWOWRWTVCVDABCDEFGHIJKLM
      NPQRSTUAUBUCUDUEVEVFWJYIYJVGZXAWBWGUUBWIWBWDUUBWFWBWDUUBWBWDYIYJABCOGHIYH
      RSTXMYGXEXLXCXDGIRTVHHJSUAVHVIXHXKXFXGGKRUDVHHLSUEVHVIZXIXJIKTUDVHJLUAUEV
      HVIZVHVIYAYFXPXTXNXOGPRUBVHHQSUCVHVIXHXSUUCXQXRPKUBUDVHQLUCUEVHVIZVHVIYDY
      EYBYCIPTUBVHJQUAUCVHVIXKXSUUDUUEVHVIVHVIVJVKVLVMVMVNVO $.
      $( [4-May-2000] $)

    ${
      $d A h f g u v w x y z $.  $d B h f g u v w x y z $.
      $d C h f g u v w x y z $.  $d D h f g u v w x y z $.
      $d F h f g u v w x y z $.  $d G h f g u v w x y z $.
      $d R h f g u v w x y z $.  $d S h f g u v w x y z $.
      $( Lemma for orthoarguesian law 5OA. $)
      5oalem7 $p |- ( ( ( A +H B ) i^i ( C +H D ) ) i^i
              ( ( F +H G ) i^i ( R +H S ) ) ) (_ ( B +H ( A i^i ( C +H
                 ( ( ( ( A +H C ) i^i ( B +H D ) ) i^i
                     ( ( ( A +H R ) i^i ( B +H S ) ) +H
                       ( ( C +H R ) i^i ( D +H S ) ) ) )
                i^i ( ( ( ( A +H F ) i^i ( B +H G ) ) i^i
                        ( ( ( A +H R ) i^i ( B +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) )
                   +H ( ( ( C +H F ) i^i ( D +H G ) ) i^i
                        ( ( ( C +H R ) i^i ( D +H S ) ) +H
                          ( ( F +H R ) i^i ( G +H S ) ) ) ) ) ) ) ) ) $=
        ( vh vx vy vz vw vf vg vv vu cph co cin cv wcel wa cva wceq wex ee4anv
        exrot4 2exbii bitr elin wrex shsel r2ex anbi12i 3bitr4r bitr4 5oalem6
        19.23aivv sylbi ssriv ) QABUFUGZCDUFUGZUHZGHUFUGZEFUFUGZUHZUHZBACACUFUG
        BDUFUGUHAEUFUGBFUFUGUHZCEUFUGDFUFUGUHZUFUGUHAGUFUGBHUFUGUHVQGEUFUGHFUFU
        GUHZUFUGUHCGUFUGDHUFUGUHVRVSUFUGUHUFUGUHUFUGUHUFUGZQUIZVPUJZRUIZAUJSUIZ
        BUJUKWAWCWDULUGUMZUKZTUIZCUJUAUIZDUJUKWAWGWHULUGUMZUKZUKZUBUIZGUJUCUIZH
        UJUKWAWLWMULUGUMZUKZUDUIZEUJUEUIZFUJUKWAWPWQULUGUMZUKZUKZUKZUEUNUDUNZUC
        UNUBUNZUAUNTUNZSUNRUNZWAVTUJZWKUAUNTUNZWTUEUNUDUNZUKZUCUNUBUNZSUNRUNXGS
        UNRUNZXHUCUNUBUNZUKZXEWBXGXHRSUBUCUOXDXJRSXDXBUAUNTUNZUCUNUBUNXJXBTUAUB
        UCUPXNXIUBUCWKWTTUAUDUEUOUQURUQWBWAVLUJZWAVOUJZUKXMWAVLVOUSXKXOXLXPWAVJ
        UJZWAVKUJZUKWFSUNRUNZWJUAUNTUNZUKXOXKXQXSXRXTXQWESBUTRAUTXSRSABWAIJVAWE
        RSABVBURXRWIUADUTTCUTXTTUACDWAKLVAWITUACDVBURVCWAVJVKUSWFWJRSTUAUOVDWAV
        MUJZWAVNUJZUKWOUCUNUBUNZWSUEUNUDUNZUKXPXLYAYCYBYDYAWNUCHUTUBGUTYCUBUCGH
        WAMNVAWNUBUCGHVBURYBWRUEFUTUDEUTYDUDUEEFWAOPVAWRUDUEEFVBURVCWAVMVNUSWOW
        SUBUCUDUEUOVDVCVEVDXDXFRSXCXFTUAXBXFUBUCXAXFUDUERSTUAUDUEABCDEFUBUCQGHI
        JKLMNOPVFVGVGVGVGVHVI $.
        $( [4-May-2000] $)
    $}
  $}

  ${
    5oa.1 $e |- A e. CH $.
    5oa.2 $e |- B e. CH $.
    5oa.3 $e |- C e. CH $.
    5oa.4 $e |- D e. CH $.
    5oa.5 $e |- F e. CH $.
    5oa.6 $e |- G e. CH $.
    5oa.7 $e |- R e. CH $.
    5oa.8 $e |- S e. CH $.
    5oa.9 $e |- A (_ ( _|_ ` B ) $.
    5oa.10 $e |- C (_ ( _|_ ` D ) $.
    5oa.11 $e |- F (_ ( _|_ ` G ) $.
    5oa.12 $e |- R (_ ( _|_ ` S ) $.
    $( Orthoarguesian law 5OA.  This 8-variable inference is called 5OA
       because it can be converted to a 5-variable equation (see
       Quantum Logic Explorer). $)
    5oa $p |- ( ( ( A vH B ) i^i ( C vH D ) ) i^i
            ( ( F vH G ) i^i ( R vH S ) ) ) (_ ( B vH ( A i^i ( C vH
               ( ( ( ( A vH C ) i^i ( B vH D ) ) i^i
                   ( ( ( A vH R ) i^i ( B vH S ) ) vH
                     ( ( C vH R ) i^i ( D vH S ) ) ) )
              i^i ( ( ( ( A vH F ) i^i ( B vH G ) ) i^i
                      ( ( ( A vH R ) i^i ( B vH S ) ) vH
                        ( ( F vH R ) i^i ( G vH S ) ) ) )
                 vH ( ( ( C vH F ) i^i ( D vH G ) ) i^i
                      ( ( ( C vH R ) i^i ( D vH S ) ) vH
                        ( ( F vH R ) i^i ( G vH S ) ) ) ) ) ) ) ) ) $=
      ( chj co cin cph cort cfv wss wceq osum ax-mp ineq12i chshi 5oalem7
      eqsstr3 shscl shincl shslej chslej ss2in mp2an shjshcl shlej2 shlej1
      sstri chjcl chincl shjcl sslin ) ABUAUBZCDUAUBZUCZGHUAUBZEFUAUBZUCZUCZBAC
      ACUDUBZBDUDUBZUCZAEUDUBZBFUDUBZUCZCEUDUBZDFUDUBZUCZUDUBZUCZAGUDUBZBHUDUBZ
      UCZWAGEUDUBZHFUDUBZUCZUDUBZUCZCGUDUBZDHUDUBZUCZWDWLUDUBZUCZUDUBZUCZUDUBZU
      CZUDUBZBACACUAUBZBDUAUBZUCZAEUAUBZBFUAUBZUCZCEUAUBZDFUAUBZUCZUAUBZUCZAGUA
      UBZBHUAUBZUCZXJGEUAUBZHFUAUBZUCZUAUBZUCZCGUAUBZDHUAUBZUCZXMYAUAUBZUCZUAUB
      ZUCZUAUBZUCZUAUBZVOABUDUBZCDUDUBZUCZGHUDUBZEFUDUBZUCZUCXDYPVKYSVNYNVIYOVJ
      ABUEUFUGYNVIUHQABIJUIUJCDUEUFUGYOVJUHRCDKLUIUJUKYQVLYRVMGHUEUFUGYQVLUHSGH
      MNUIUJEFUEUFUGYRVMUHTEFOPUIUJUKUKABCDEFGHAIULZBJULZCKULZDLULZGMULZHNULZEO
      ULZFPULZUMUNXDBXCUAUBZYMBXCUUAAXBYTCXAUUBWFWTVRWEVPVQACYTUUBUOBDUUAUUCUOU
      PWAWDVSVTAEYTUUFUOBFUUAUUGUOUPZWBWCCEUUBUUFUODFUUCUUGUOUPZUOUPWNWSWIWMWGW
      HAGYTUUDUOBHUUAUUEUOUPWAWLUUIWJWKGEUUDUUFUOHFUUEUUGUOUPZUOUPZWQWRWOWPCGUU
      BUUDUODHUUCUUEUOUPWDWLUUJUUKUOUPZUOUPZUOUPZUQXCYLUGZUUHYMUGXBYKUGUUPXBCXA
      UAUBZYKCXAUUBUUNUQXAYJUGZUUQYKUGWFXOUGZWTYIUGUURVRXGUGZWEXNUGUUSVPXEUGVQX
      FUGUUTACIKURBDJLURVPXEVQXFUSUTWEWAWDUAUBZXNWAWDUUIUUJUQUVAWAXMUAUBZXNWDXM
      UGZUVAUVBUGWBXKUGWCXLUGUVCCEKOURDFLPURWBXKWCXLUSUTZWDXMWAUUJXKXLCEUUBUUFV
      ADFUUCUUGVAUPZUUIVBUJWAXJUGZUVBXNUGVSXHUGVTXIUGUVFAEIOURBFJPURVSXHVTXIUSU
      TZWAXJXMUUIXHXIAEYTUUFVABFUUAUUGVAUPZUVEVCUJVDVDVRXGWEXNUSUTWTWNWSUAUBZYI
      WNWSUULUUMUQUVIWNYHUAUBZYIWSYHUGZUVIUVJUGWQYFUGZWRYGUGUVKWOYDUGWPYEUGUVLC
      GKMURDHLNURWOYDWPYEUSUTWRWDWLUAUBZYGWDWLUUJUUKUQUVMWDYAUAUBZYGWLYAUGZUVMU
      VNUGWJXSUGWKXTUGUVOGEMOURHFNPURWJXSWKXTUSUTZWLYAWDUUKXSXTGEUUDUUFVAHFUUEU
      UGVAUPZUUJVBUJUVCUVNYGUGUVDWDXMYAUUJUVEUVQVCUJVDVDWQYFWRYGUSUTWSYHWNUUMYF
      YGYFYDYECGKMVEDHLNVEVFULXMYAUVEUVQVAUPZUULVBUJWNYCUGZUVJYIUGWIXRUGZWMYBUG
      UVSWGXPUGWHXQUGUVTAGIMURBHJNURWGXPWHXQUSUTWMWAWLUAUBZYBWAWLUUIUUKUQUWAWAY
      AUAUBZYBUVOUWAUWBUGUVPWLYAWAUUKUVQUUIVBUJUVFUWBYBUGUVGWAXJYAUUIUVHUVQVCUJ
      VDVDWIXRWMYBUSUTWNYCYHUULXRYBXRXPXQAGIMVEBHJNVEVFULXJYAUVHUVQVAUPZUVRVCUJ
      VDVDWFXOWTYIUSUTXAYJCUUNXOYIXOXGXNXEXFACIKVEBDJLVEVFXJXMUVHUVEVGVFULYCYHU
      WCUVRVAUPZUUBVBUJVDXBYKAVHUJXCYLBUUOAYKYTCYJUUBUWDVAUPUUAVBUJVDVD $.
      $( [5-May-2000] $)
  $}

  ${
    $d x y z w v B $.  $d x y z w v C $.  $d x y z w v R $.  $d x y z w v S $.
    3oalem1.1 $e |- B e. CH $.
    3oalem1.2 $e |- C e. CH $.
    3oalem1.3 $e |- R e. CH $.
    3oalem1.4 $e |- S e. CH $.
    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem1 $p |- ( ( ( ( x e. B /\ y e. R ) /\ v = ( x +h y ) ) /\
                  ( ( z e. C /\ w e. S ) /\ v = ( z +h w ) ) ) ->
                 ( ( ( x e. H~ /\ y e. H~ ) /\ v e. H~ ) /\
                  ( z e. H~ /\ w e. H~ ) ) ) $=
      ( cv wcel wa cva co wceq chil eleq1 hvaddclt syl5cbir imdistani chel
      anim12i sylan adantr ) ANZFOZBNZHOZPZENZUIUKQRZSZPUITOZUKTOZPZUNTOZPZCNZG
      OZDNZIOZPZUNVBVDQRSZPVBTOZVDTOZPZUSUPVAUMUSUPUTUPUTUOTOUSUNUOTUAUIUKUBUCU
      DUJUQULURUIFJUEUKHLUEUFUGVFVJVGVCVHVEVIVBGKUEVDIMUEUFUHUF $.
      $( [19-Oct-1999] $)

    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem2 $p |- ( ( ( ( x e. B /\ y e. R ) /\ v = ( x +h y ) ) /\
                  ( ( z e. C /\ w e. S ) /\ v = ( z +h w ) ) ) ->
            v e. ( B +H ( R i^i ( S +H ( ( B +H C ) i^i ( R +H S ) ) ) ) ) ) $=
      ( cv wcel wa cva co wceq cph cin chshi shscl shincl shsva pm3.26
      ad2antrr pm3.27 cmv chil 3oalem1 hvaddsub12t 3anidm23 c0v hvsubidt
      opreq2d ax-hvaddid sylan9eqr eqtr3d ad2ant2l adantlr syl ad2antrl eqtr2t
      opreq1d anim1i hvsub4t syldan hvsubclt hvaddid2t adantll 3eqtrd
      ad2ant2rl anim2i sylanc ad2antll ancoms adantrr 3eqtr3d shsvs ancom
      shscom eleq2i 3imtr4 simpll syl2an eqeltrd simplr jca elin sylibr
      eqeltrrd wb eleq1 ad2antlr mpbird ) ANZFOZBNZHOZPZENZWQWSQRZSZPZCNZGOZDNZ
      IOZPZXBXFXHQRZSZPZPZXBFHIFGTRZHITRZUAZTRZUAZTRZOZXCXTOZWRWSXSOZYBXNFXSWQW
      SFJUBZHXRHLUBZIXQIMUBZXOXPFGYDGKUBZUCHIYEYFUCUDZUCUDUEXAWRXDXMWRWTUFUGXNW
      TWSXROZPYCXNWTYIXAWTXDXMWRWTUHUGXNXHWSXHUIRZQRZWSXRXNWQUJOZWSUJOZPZXBUJOZ
      PXFUJOZXHUJOZPZPZYKWSSZABCDEFGHIJKLMUKZYNYRYTYOYMYQYTYLYPYMYQPZWSXHXHUIRZ
      QRZYKWSYMYQUUDYKSWSXHXHULUMYQYMUUDWSUNQRWSYQUUCUNWSQXHUOZUPWSUQURUSUTVAVB
      XIYJXQOZYKXROXNIXQXHYJYFYHUEXJXIXEXLXGXIUHVCXNYJXOOZYJXPOZPUUFXNUUGUUHXNY
      JXFWQUIRZXOXNXCWQXHQRZUIRZXKUUJUIRZYJUUIXDXLUUKUULSXAXJXDXLPXCXKUUJUIXBXC
      XKVDVEUTXNYSUUKYJSZUUAYNYQUUMYOYPYNYQPZUUKWQWQUIRZYJQRZUNYJQRZYJYNYQYLYQP
      ZUUKUUPSYNYLYQYLYMUFVFWQWSWQXHVGVHUUNUUOUNYJQYLUUOUNSYMYQWQUOUGVEYMYQUUQY
      JSZYLUUBYJUJOUUSWSXHVIYJVJVBVKVLVMVBXNYSUULUUISZUUAYNYRUUTYOYLYRUUTYMYLYR
      PZUULUUIUUCQRZUUIUNQRZUUIYRUURUULUVBSUVAXFXHWQXHVGYLYRUHYRYQYLYPYQUHVNVOU
      VAUUCUNUUIQYQUUCUNSYLYPUUEVPUPYLYPUVCUUISZYQYPYLUVDYPYLPUUIUJOUVDXFWQVIUU
      IUQVBVQVRVLVAVAVBVSWRXGUUIXOOZXEXMXGWRPUUIGFTRZOWRXGPUVEGFXFWQYGYDVTWRXGW
      AXOUVFUUIFGYDYGWBWCWDWRWTXDWEXGXIXLWEWFWGWTXIUUHXEXMHIWSXHYEYFVTWRWTXDWHX
      GXIXLWHWFWIYJXOXPWJWKVOWLWIWSHXRWJWKVOXDYAYBWMXAXMXBXCXTWNWOWP $.
      $( [19-Oct-1999] $)

    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem3 $p |- ( ( B +H R ) i^i ( C +H S ) ) (_
                   ( B +H ( R i^i ( S +H ( ( B +H C ) i^i ( R +H S ) ) ) ) ) $=
      ( vv vx vy vz vw cph co cin cv wcel wa cva wceq wex wrex chsel r2ex bitr
      anbi12i elin ee4anv 3bitr4 3oalem2 19.23aivv sylbi ssriv ) IACNOZBDNOZPZA
      CDABNOCDNOPNOPNOZIQZUQRZJQZARKQZCRSUSVAVBTOUAZSZLQZBRMQZDRSUSVEVFTOUAZSZS
      ZMUBLUBZKUBJUBZUSURRZUSUORZUSUPRZSVDKUBJUBZVHMUBLUBZSUTVKVMVOVNVPVMVCKCUC
      JAUCVOJKACUSEGUDVCJKACUEUFVNVGMDUCLBUCVPLMBDUSFHUDVGLMBDUEUFUGUSUOUPUHVDV
      HJKLMUIUJVJVLJKVIVLLMJKLMIABCDEFGHUKULULUMUN $.
      $( [19-Oct-1999] $)
  $}

  ${
    3oalem4.3 $e |- R = ( ( _|_ ` B ) i^i ( B vH A ) ) $.
    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem4 $p |- R (_ ( _|_ ` B ) $=
      ( cort cfv chj co cin inss1 eqsstr ) CBEFZBAGHZILDLMJK $.
      $( [19-Oct-1999] $)
  $}

  ${
    3oa.1 $e |- A e. CH $.
    3oa.2 $e |- B e. CH $.
    3oa.3 $e |- C e. CH $.
    3oa.4 $e |- R = ( ( _|_ ` B ) i^i ( B vH A ) ) $.
    3oa.5 $e |- S = ( ( _|_ ` C ) i^i ( C vH A ) ) $.
    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem5 $p |- ( ( B +H R ) i^i ( C +H S ) ) =
                  ( ( B vH R ) i^i ( C vH S ) ) $=
      ( cph co chj cort cfv wss wceq 3oalem4 cin cch choccl chjcl chincl
      eqeltr osum ax-mp chshi shscom chjcom 3eqtr4 ineq12i ) BDKLZBDMLZCEKLZCEM
      LZDBKLZDBMLZULUMDBNOZPUPUQQABDIRDBDURBAMLZSTIURUSBGUABAGFUBUCUDZGUEUFBDBG
      UGDUTUGUHBDGUTUIUJECKLZECMLZUNUOECNOZPVAVBQACEJRECEVCCAMLZSTJVCVDCHUACAHF
      UBUCUDZHUEUFCECHUGEVEUGUHCEHVEUIUJUK $.
      $( [19-Oct-1999] $)

    $( Lemma for 3OA (weak) orthoarguesian law. $)
    3oalem6 $p |- ( B +H ( R i^i ( S +H ( ( B +H C ) i^i ( R +H S ) ) ) ) ) (_
                  ( B vH ( R i^i ( S vH ( ( B vH C ) i^i ( R vH S ) ) ) ) ) $=
      ( cph co cin chj chshi cort cfv cch choccl chjcl chincl eqeltr shscl
      shincl shslej wss chslej ssrin ax-mp sslin sstri shlej2 ) BDEBCKLZDEKLZMZ
      KLZMZKLBUQNLZBDEBCNLZDENLZMZNLZMZNLZBUQBGOZDUPDDBPQZBANLZMRIVFVGBGSBAGFTU
      AUBZOZEUOEECPQZCANLZMRJVJVKCHSCAHFTUAUBZOZUMUNBCVECHOUCDEVIVMUCUDZUCUDZUE
      UQVCUFZURVDUFUPVBUFVPUPEUONLZVBEUOVMVNUEUOVAUFVQVBUFUOUSUNMZVAUMUSUFUOVRU
      FBCGHUGUMUSUNUHUIUNUTUFVRVAUFDEVHVLUGUNUTUSUJUIUKUOVAEVNVAUSUTBCGHTDEVHVL
      TUAZOVMULUIUKUPVBDUJUIUQVCBVOVCDVBVHEVAVLVSTUAOVEULUIUK $.
      $( [19-Oct-1999] $)

    $( 3OA (weak) orthoarguesian law.  Equation IV of [GodowskiGreechie]
       p. 249. $)
    3oa $p |- ( ( B vH R ) i^i ( C vH S ) ) (_
                  ( B vH ( R i^i ( S vH ( ( B vH C ) i^i ( R vH S ) ) ) ) ) $=
      ( chj co cin cph 3oalem5 cort cfv cch choccl chjcl chincl eqeltr 3oalem3
      3oalem6 sstri eqsstr3 ) BDKLCEKLMBDNLCENLMZBDEBCKLDEKLMKLMKLZABCDEFGHIJOU
      GBDEBCNLDENLMNLMNLUHBCDEGHDBPQZBAKLZMRIUIUJBGSBAGFTUAUBECPQZCAKLZMRJUKULC
      HSCAHFTUAUBUCABCDEFGHIJUDUEUF $.
      $( [19-Oct-1999] $)
  $}

