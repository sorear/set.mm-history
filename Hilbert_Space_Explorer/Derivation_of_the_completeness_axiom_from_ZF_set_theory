$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Derivation of the completeness axiom from ZF set theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    hilmet.1 $e |- D = ( normh o. -h ) $.
    $( The Hilbert space norm determines a metric space. $)
    hilmet $p |- D e. Met $=
      ( cva csm cop cno eqid hhims hhmet ) ACDEFEZJGZAJKBHI $.
      $( [17-Apr-2007] $)

    $( Value of the distance function of the metric space of Hilbert space. $)
    hilmetdval $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( A D B ) = ( normh ` ( A -h B ) ) ) $=
      ( cva csm cop cno eqid hhims hhmetdval ) ABCEFGHGZLIZCLMDJK $.
      $( [17-Apr-2007] $)

    $( The base set for the metric for Hilbert space. $)
    hilmetba $p |- ~H = dom dom D $=
      ( cva csm cop cno eqid hhims hhmetba ) ACDEFEZJGZAJKBHI $.
      $( [6-Sep-2007] $)
  $}

  ${
    $d x y z U $.
    hilims.1 $e |- ~H = ( BaseSet ` U ) $.
    hilims.2 $e |- +h = ( +v ` U ) $.
    hilims.3 $e |- .h = ( .s ` U ) $.
    hilims.5 $e |- .ih = ( .i ` U ) $.
    hilims.8 $e |- D = ( IndMet ` U ) $.
    hilims.9 $e |- U e. NrmCVec $.
    $( Hilbert space distance metric. $)
    hilims $p |- D = ( normh o. -h ) $=
      ( hilhhi hhims2 ) ABBCDEFHIGJ $.
      $( [13-Sep-2007] $)
  $}

  ${
    $d f w x y z D $.
    hillim.1 $e |- ~H = ( BaseSet ` U ) $.
    hillim.2 $e |- +h = ( +v ` U ) $.
    hillim.3 $e |- .h = ( .s ` U ) $.
    hillim.5 $e |- .ih = ( .i ` U ) $.
    hillim.8 $e |- D = ( IndMet ` U ) $.
    hillim.9 $e |- U e. NrmCVec $.
    $( MAY BE REVISED - not needed? delete? $)
    $( Hilbert space limit in terms of metric space limit. $)
    hillim $p |- ~~>v = ( ( ~~>m ` D ) |` ( ~H ^m NN ) ) $=
      ( hilhhi h2hlm ) ABBCDEFHIHCGJ $.
      $( [13-Sep-2007] $)

    $( MAY BE REVISED - not needed? delete? $)
    $( Hilbert space Cauchy sequences in terms of metric space Cauchy
       sequences. $)
    hilcau $p |- Cauchy = ( ( Cau ` D ) i^i ( ~H ^m NN ) ) $=
      ( hilhhi h2hcau ) ABBCDEFHIHCGJ $.
      $( [10-Sep-2007] $)
  $}

  ${
    hhlm.1 $e |- U = <. <. +h , .h >. , normh >. $.
    hhlm.2 $e |- D = ( IndMet ` U ) $.
    $( The limit sequences of Hilbert space. $)
    hhlm $p |- ~~>v = ( ( ~~>m ` D ) |` ( ~H ^m NN ) ) $=
      ( hhnv hhba h2hlm ) ABCBCEBCFDG $.
      $( [19-Nov-2007] $)

    $( The Cauchy sequences of Hilbert space. $)
    hhcau $p |- Cauchy = ( ( Cau ` D ) i^i ( ~H ^m NN ) ) $=
      ( hhnv hhba h2hcau ) ABCBCEBCFDG $.
      $( [19-Nov-2007] $)

    ${
      $d x F $.
      hhcmpl.c $e |- ( F e. ( Cau ` D ) -> E. x e. ~H F ( ~~>m ` D ) x ) $.
      $( Lemma used for derivation of the completeness axiom ~ ax-hcompl from
         ZFC Hilbert space theory. $)
      hhcmpl $p |- ( F e. Cauchy -> E. x e. ~H F ~~>v x ) $=
        ( cca cfv wcel chil cn cmap co wa wbr wrex ccau chli bitri cv clm hhcau
        anim1i eleq2i elin cres hhlm breqi visset brres rexbii r19.41v 3imtr4i
        cin ) DBHIZJZDKLMNZJZOZDAUAZBUBIZPZAKQZUSOZDRJZDVASPZAKQZUQVDUSGUDVFDUP
        URUOZJUTRVIDBCEFUCUEDUPURUFTVHVCUSOZAKQVEVGVJAKVGDVAVBURUGZPVJDVASVKBCE
        FUHUIDVAVBURAUJUKTULVCUSAKUMTUN $.
        $( [9-Apr-2008] $)
    $}
  $}

  ${
    $d x F $.
    hilcompl.1 $e |- ~H = ( BaseSet ` U ) $.
    hilcompl.2 $e |- +h = ( +v ` U ) $.
    hilcompl.3 $e |- .h = ( .s ` U ) $.
    hilcompl.5 $e |- .ih = ( .i ` U ) $.
    hilcompl.8 $e |- D = ( IndMet ` U ) $.
    hilcompl.9 $e |- U e. CHil $.
    hilcompl.c $e |- ( F e. ( Cau ` D ) -> E. x e. ~H F ( ~~>m ` D ) x ) $.
    $( Lemma used for derivation of the completeness axiom ~ ax-hcompl from ZFC
       Hilbert space theory.  The first 5 hypotheses would be satisfied by the
       definitions described in ~ ax-hilex ; the 6th would be satisfied by
       ~ eqid ; the 7th by a given fixed Hilbert space; and the last by theorem
       ~ hlcompl . $)
    hilcompl $p |- ( F e. Cauchy -> E. x e. ~H F ~~>v x ) $=
      ( hlnvi hilhhi hhcmpl ) ABCDCEFGHCJLMIKN $.
      $( [13-Sep-2007] $)
  $}

