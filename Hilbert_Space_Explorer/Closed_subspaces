$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Closed subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x f h $.
    $( Define the set of closed subspaces of a Hilbert space.  A closed
       subspace is one in which the limit of every convergent sequence
       in the subspace belongs to the subspace.  For its
       membership relation, see ~ closedsub .  From Definition of [Beran]
       p. 107.  Alternate definitions are given by ~ chcmh and ~ dfch2 . $)
    df-ch $a |- CH = { h | ( h e. SH /\ A. f A. x ( ( f : NN --> h /\
        f ~~>v x ) -> x e. h ) ) } $.
  $}

  ${
    $d x f h H $.
    $( Closed subspace ` H ` of a Hilbert space.  Definition of [Beran]
       p. 107. $)
    closedsub $p |- ( H e. CH <-> ( H e. SH /\ A. f A. x ( ( f : NN --> H /\
        f ~~>v x ) -> x e. H ) ) ) $=
      ( vh cch wcel cvv csh cn cv wf chli wbr wa wi wal elisset adantr wceq
      eleq1 feq3 anbi1d eleq2 imbi12d 2albidv anbi12d df-ch elab2g pm5.21nii )
      CEFCGFZCHFZICBJZKZULAJZLMZNZUNCFZOZAPBPZNZCEQUKUJUSCHQRDJZHFZIVAULKZUONZU
      NVAFZOZAPBPZNUTDCEGVACSZVBUKVGUSVACHTVHVFURBAVHVDUPVEUQVHVCUMUOVACIULUAUB
      VACUNUCUDUEUFABDUGUHUI $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x f h $.
    $( Closed subspaces are subspaces in a Hilbert space. $)
    chsssh $p |- CH (_ SH $=
      ( vh vf vx cch cv csh wcel cn wf chli wbr wa wi wal cab df-ch ssab2
      eqsstr ) DAEZFGHSBEZITCEZJKLUASGMCNBNZLAOFCBAPUBAFQR $.
      $( [29-May-1999] $)
  $}

  $( The set of closed subspaces of a Hilbert space exists (is a set). $)
  chex $p |- CH e. V $=
    ( cch csh shex chsssh ssexi ) ABCDE $.
    $( [23-Oct-1999] $)

  $( A closed subspace is a subspace. $)
  chsh $p |- ( H e. CH -> H e. SH ) $=
    ( cch csh chsssh sseli ) BCADE $.
    $( [19-Oct-1999] $)

  ${
    chshi.1 $e |- H e. CH $.
    $( A closed subspace is a subspace. $)
    chshi $p |- H e. SH $=
      ( cch wcel csh chsh ax-mp ) ACDAEDBAFG $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d x y H $.
    $( The zero vector belongs to any closed subspace of a Hilbert space. $)
    ch0 $p |- ( H e. CH -> 0h e. H ) $=
      ( cch wcel csh c0v chsh sh0 syl ) ABCADCEACAFAGH $.
      $( [24-Aug-1999] $)

    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chss $p |- ( H e. CH -> H (_ H~ ) $=
      ( cch wcel csh chil wss chsh shss syl ) ABCADCAEFAGAHI $.
      $( [24-Aug-1999] $)

  $}

  $( A member of a closed subspace of a Hilbert space is a vector. $)
  chelt $p |- ( ( H e. CH /\ A e. H ) -> A e. H~ ) $=
    ( cch wcel chil chss sseld imp ) BCDZABDAEDIBEABFGH $.
    $( [17-Dec-2004] $) $( [15-Dec-2004] $)

  ${
    chssi.1 $e |- H e. CH $.
    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chssi $p |- H (_ H~ $=
      ( chshi shssi ) AABCD $.
      $( [6-Oct-1999] $)

    $( A member of a closed subspace of a Hilbert space is a vector. $)
    chel $p |- ( A e. H -> A e. H~ ) $=
      ( chil chssi sseli ) BDABCEF $.
      $( [6-Oct-1999] $)

    ${
      cheli.1 $e |- A e. H $.
      $( A member of a closed subspace of a Hilbert space is a vector. $)
      cheli $p |- A e. H~ $=
        ( chil chssi sselii ) BEABCFDG $.
        $( [6-Oct-1999] $)
    $}
  $}

  ${
    $d x f F $.  $d x f H $.  $d x A $.
    chlim.1 $e |- A e. V $.
    $( The limit property of a closed subspace of a Hilbert space. $)
    chlim $p |- ( H e. CH -> ( ( F : NN --> H /\ F ~~>v A ) -> A e. H ) ) $=
      ( vf vx cch wcel cn cv wf chli wbr wa wi wal csh closedsub pm3.27bi cvv
      nnex fex mpan2 adantr wceq feq1 breq1 anbi12d imbi1d albidv cla4gv breq2
      anbi2d eleq1 imbi12d cla4v syl6 syl pm2.43b ) CGHZICEJZKZVAFJZLMZNZVCCHZO
      ZFPZEPZICBKZBALMZNZACHZOZUTCQHVIFECRSVIVLVMVLBTHZVIVNOVJVOVKVJITHVOUAICTB
      UBUCUDVOVIVJBVCLMZNZVFOZFPZVNVHVSEBTVABUEZVGVRFVTVEVQVFVTVBVJVDVPICVABUFV
      ABVCLUGUHUIUJUKVRVNFADVCAUEZVQVLVFVMWAVPVKVJVCABLULUMVCACUNUOUPUQURUSUR
      $.
      $( [14-Sep-1999] $)
  $}

  ${
    $d x y z $.
    $( The zero sequence in Hilbert space converges to the zero vector. $)
    hlim0 $p |- ( NN X. { 0h } ) ~~>v 0h $=
      ( vx vy vz cn c0v csn cxp chli wbr chil wf wcel wa cc0 cv clt cle cfv
      cmv co cno wi wral wrex cr nnex snex xpex ax-hv0cl elisseti hlim wss
      fconst snssi ax-mp fss mp2an pm3.2i c1 fvconst2 opreq1d wceq hvsubidt
      syl6eq fveq2d norm0 breq1d biimprd a1d com3r r19.21aiv 1nn jctil breq1
      imbi1d ralbidv rcla4ev syl a1i rgen mpbir2an ) DEFZGZEHIDJWCKZEJLZMNAOZPI
      ZBOZCOZQIZWIWCRZESTZUARZWFPIZUBZCDUCZBDUDZUBZAUEUCABCEWCDWBUFEUGUHEJUIUJZ
      UKWDWEDWBWCKWBJULZWDDEWSUMWEWTUIEJUNUODWBJWCUPUQUIURWRAUEWRWFUELWGUSDLZUS
      WIQIZWNUBZCDUCZMWQWGXDXAWGXCCDWIDLZXBWGWNXEWGWNUBXBXEWNWGXEWMNWFPXEWMEUAR
      NXEWLEUAXEWLEESTZEXEWKEESDEWIWSUTVAWEXFEVBUIEVCUOVDVEVFVDVGVHVIVJVKVLVMWP
      XDBUSDWHUSVBZWOXCCDXGWJXBWNWHUSWIQVNVOVPVQVRVSVTWA $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x y z w v A $.  $d x y z w v F $.
    hlimcau.1 $e |- A e. V $.
    hlimcau.2 $e |- F e. V $.
    ${
      hlimcaui.4 $e |- F ~~>v A $.
      $( If a sequence in Hilbert space subset converges to a limit, it is a
         Cauchy sequence. $)
      hlimcaui $p |- F e. Cauchy $=
        ( vx vy vz vw vv ccau wcel cn chil wf cc0 cv clt wbr cle wa cfv cmv co
        cno wi wral wrex cr hcau chli hlimseq ax-mp c2 cdiv wceq breq2 imbi2d
        rexralbidv imbi12d hlimvec hlim2 mpbii mp2an vtoclri rehalfclt adantr
        cif opreq1 breq2d 0re elimel 2re 2pos divgt0i2 dedth imp sylc prth wb
        normsubt breq1d anbi2d adantl ffvelrni anim1i ancoms an4s norm3lemt
        syl sylbird jctil sylan2 syl9r r19.20dva raaan fveq2 opreq1d fveq2d
        cbvralv anbi2i anidm 3bitr syl5ibr r19.22sdv mpd ex rgen mpbir2an ) BKL
        MNBOZPFQZRSZGQZHQZTSZXMIQZTSZUAZXNBUBZXPBUBZUCUDUEUBXKRSZUFZIMUGZHMUGZG
        MUHZUFZFUIUGFGHIBUJBAUKSZXJEABDCULUMZYFFUIXKUILZXLYEYIXLUAZXOXSAUCUDZUE
        UBZXKUNUOUDZRSZUFZHMUGZGMUHZYEYMUILZPYMRSZYQYJPJQZRSZXOYLYTRSZUFZHMUGGM
        UHZUFZYSYQUFJYMUIYTYMUPZUUAYSUUDYQYTYMPRUQUUFUUCYOGHMMUUFUUBYNXOYTYMYLR
        UQURUSUTXJANLZUUEJUIUGZYHYGUUGEABDCVAUMZXJUUGUAYGUUHEJGHABVBVCVDVEYIYRX
        LXKVFVGYIXLYSYIXLYSUFPYIXKPVHZRSZPUUJUNUOUDZRSZUFXKPXKUUJUPZXLUUKYSUUMX
        KUUJPRUQUUNYMUULPRXKUUJUNUOVIVJUTUUJUNXKPUIVKVLVMVNVOVPVQVRYIYQYEUFXLYI
        YPYDGMYIYOXQXTAUCUDZUEUBZYMRSZUFZUAZIMUGZHMUGZYDYPYIUUTYCHMYIXNMLZUAZUU
        SYBIMUUSXRYNUUQUAZUVCXPMLZUAYAXOYNXQUUQVSUVCUUGXTNLZUAZUVDYAUFUVEUVCUVG
        UAZUVDYNAXTUCUDUEUBZYMRSZUAZYAUVGUVKUVDVTUVCUVGUVJUUQYNUVGUVIUUPYMRAXTW
        AWBWCWDUVHXSNLZUVFUAZUUGYIUAZUAZUVKYAUFUVGUVCUVOUUGYIUVFUVBUVOUVFUVBUAZ
        UVNUVOUVPUVMUVNUVBUVFUVMUVBUVLUVFMNXNBYHWEWFWGWFWGWHWGXSXTAXKWIWJWKUVEU
        VFUUGMNXPBYHWEUUIWLWMWNWOWOUVAYPUURIMUGZUAYPYPUAYPYOUURHIMWPUVQYPYPUURY
        OIHMXPXNUPZXQXOUUQYNXPXNXMTUQUVRUUPYLYMRUVRUUOYKUEUVRXTXSAUCXPXNBWQWRWS
        WBUTWTXAYPXBXCXDXEVGXFXGXHXI $.
        $( [17-Aug-1999] $)
    $}

    $( If a sequence in Hilbert space subset converges to a limit, it is a
       Cauchy sequence. $)
    hlimcau $p |- ( F ~~>v A -> F e. Cauchy ) $=
      ( chli wbr ccau wcel cn c0v csn cxp cif eleq1 chil ax-hv0cl elisseti
      ifex nnex snex xpex breq1 breq2 hlim0 elimhyp2v hlimcaui dedth ) BAEFZBGH
      UHBIJKZLZMZGHBUJBUKGNUHAJMZUKUHAJCJOPQRUHBUJDIUISJTUARUHUKAEFUKULEFUJJEFB
      AUJJUKJEFBUKAEUBAULUKEUCUJUKJEUBJULUKEUCUDUEUFUG $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x y z w v A $.  $d x y z w v B $.  $d x y z w v F $.
    hlimuni.1 $e |- A e. V $.
    hlimuni.2 $e |- B e. V $.
    hlimuni.3 $e |- F e. V $.
    ${
      hlimunii.3 $e |- ( F ~~>v A /\ F ~~>v B ) $.
      $( A Hilbert space sequence converges to at most one limit. $)
      hlimunii $p |- A = B $=
        ( vy vz vw vx cmv co c0v wceq cc0 cno cfv clt wbr wn cv cle wa cn wrex
        wral nn2get rgen2a c2 cdiv wi chli chil wcel pm3.26i hlimvec ax-mp
        pm3.27i hvsubcl normcl 2re 2pos divgt0i2 cr 2ne0 redivcl hlimconv
        breq2 imbi2d rexralbidv imbi12d rcla4v mp2 jca r19.26 con3 ltnr wf
        hlimseq ffvelrni normsubt mpan2 syl breq1d anbi1d norm3lemt mpanl12
        sylbid mtoi syl5com prth syl5 r19.20i sylbir r19.22si reeanv ralnex
        rexbii rexnal bitr 3imtr3 3syl mt2 wne wb normgt0t necon1bbii mpbi
        hvsubeq0 ) ABLMZNOZABOPXKQRZSTZUAXLXNHUBZIUBZUCTZJUBZXPUCTZUDZIUEUFZJUE
        UGZHUEUGZYAHJUEIXOXRUHUIXNPXMUJUKMZSTZXQXPCRZALMQRZYDSTZULZIUEUGZHUEUFZ
        XSYFBLMQRZYDSTZULZIUEUGZJUEUFZUDZYCUAZXMUJXKABCAUMTZAUNUOZYSCBUMTZGUPZA
        CFDUQURZUUABUNUOZYSUUAGUSZBCFEUQURZUTZVAZVBVCVDYEYKYPYDVEUOZPKUBZSTZXQY
        GUUJSTZULZIUEUGHUEUFZULZKVEUGZYEYKULZXMUJUUHVBVFVGZYSUUPUUBKHIACFDVHURU
        UOUUQKYDVEUUJYDOZUUKYEUUNYKUUJYDPSVIZUUSUUMYIHIUEUEUUSUULYHXQUUJYDYGSVI
        VJVKVLVMVNUUIUUKXSYLUUJSTZULZIUEUGJUEUFZULZKVEUGZYEYPULZUURUUAUVEUUEKJI
        BCFEVHURUVDUVFKYDVEUUSUUKYEUVCYPUUTUUSUVBYNJIUEUEUUSUVAYMXSUUJYDYLSVIVJ
        VKVLVMVNVOYJYOUDZJUEUFZHUEUFXTUAZIUEUGZJUEUFZHUEUFZYQYRUVHUVKHUEUVGUVJJ
        UEUVGYIYNUDZIUEUGUVJYIYNIUEVPUVMUVIIUEXPUEUOZXTYHYMUDZULZUVIUVMUVPUVOUA
        UVIUVNXTUVOVQUVNUVOXMXMSTZXMUUHVRUVNUVOAYFLMQRZYDSTZYMUDZUVQUVNYHUVSYMU
        VNYGUVRYDSUVNYFUNUOZYGUVROZUEUNXPCYSUEUNCVSUUBACFDVTURWAZUWAYTUWBUUCYFA
        WBWCWDWEWFUVNUWAUVTUVQULZUWCUWAXMVEUOZUWDUUHYTUUDUWAUWEUDUWDUUCUUFABYFX
        MWGWHWCWDWIWJWKXQYHXSYMWLWMWNWOWPWPYJYOHJUEUEWQUVLYBUAZHUEUFYRUVKUWFHUE
        UVKYAUAZJUEUFUWFUVJUWGJUEXTIUEWRWSYAJUEWTXAWSYBHUEWTXAXBXCXDXNXKNXKUNUO
        XKNXEXNXFUUGXKXGURXHXIABUUCUUFXJXI $.
        $( [19-Aug-1999] $)
    $}

    $( A Hilbert space sequence converges to at most one limit. $)
    hlimuni $p |- ( ( F ~~>v A /\ F ~~>v B ) -> A = B ) $=
      ( chli wbr wa wceq c0v cif eqeq1 eqeq2 cn csn cxp chil ax-hv0cl elisseti
      ifex nnex snex xpex breq2 anbi1d anbi2d breq1 anbi12d hlim0 pm3.2i
      elimhyp3v hlimunii dedth2v ) CAGHZCBGHZIZABJUQAKLZBJURUQBKLZJABKKAURBMBUS
      URNURUSUQCOKPZQZLZUQAKDKRSTZUAUQBKEVCUAUQCVAFOUTUBKUCUDUAUQCURGHZUPIVDCUS
      GHZIVBURGHZVBUSGHZIABCKKVAVAKGHZVHIVAURGHZVHIVIVAUSGHZIAURJUOVDUPAURCGUEU
      FBUSJUPVEVDBUSCGUEUGCVBJVDVFVEVGCVBURGUHCVBUSGUHUIKURJVHVIVHKURVAGUEUFKUS
      JVHVJVIKUSVAGUEUGVAVBJVIVFVJVGVAVBURGUHVAVBUSGUHUIVHVHUJUJUKULUMUN $.
      $( [19-Aug-1999] $)
  $}

  ${
    $d x y F $.  $d x y H $.
    hlimreu.1 $e |- F e. V $.
    $( The limit of a Hilbert space sequence is unique. $)
    hlimreu $p |- ( E. x e. H F ~~>v x <-> E! x e. H F ~~>v x ) $=
      ( vy cv chli wbr wrex wreu wa wceq wi wral wcel visset hlimuni a1i
      rgen2a jctr breq2 reu4 sylibr reurex impbi ) BAFZGHZACIZUGACJZUHUHUGBEFZG
      HZKUFUJLMZECNACNZKUIUHUMULAECULUFCOUJCOKUFUJBAPEPDQRSTUGUKAECUFUJBGUAUBUC
      UGACUDUE $.
      $( [19-Aug-1999] $)
  $}

  ${
    $d x y F $.  $d x y H $.
    hlimeu.1 $e |- F e. V $.
    $( The limit of a Hilbert space sequence is unique. $)
    hlimeu $p |- ( E. x F ~~>v x <-> E! x F ~~>v x ) $=
      ( vy cv chli wbr wex weu wa wceq wi wal visset hlimuni gen2 jctr breq2
      eu4 sylibr euex impbi ) BAEZFGZAHZUDAIZUEUEUDBDEZFGZJUCUGKLZDMAMZJUFUEUJU
      IADUCUGBANDNCOPQUDUHADUCUGBFRSTUDAUAUB $.
      $( [19-Aug-1999] $)
  $}

  ${
    $d x f h $.  $d h C $.
    cmh.1 $e |- C = { h | ( h e. SH /\ A. f e. Cauchy (
      f : NN --> h -> E. x e. h f ~~>v x ) ) } $.
    $( The hypothesis defines the set of complete subspaces of Hilbert
       space.  A complete subspace is one in which every Cauchy sequence of
       vectors in the subspace converges to a member of the subspace
       (Definition of complete subspace in [Beran] p. 96).  Any closed
       subspace of a Hilbert space is complete.  Part of Remark 3.12 of
       [Beran] p. 107. $)
    chsscm $p |- CH (_ C $=
      ( cch cv csh wcel cn wf chli wbr wa wi wal wrex ccau wral chil wex
      impexp ancr adantld imim2i sylbi com12 19.20dv impcom 19.22 syl df-rex
      3imtr4g ax-hcompl syl5 ex com23 19.20i df-ral sylibr anim2i closedsub
      abeq2i 3imtr4 ssriv ) DFBDGZHIZJVFCGZKZVHAGZLMZNVJVFIZOZAPZCPZNVGVIVKAVFQ
      ZOZCRSZNZVFFIVFBIVOVRVGVOVHRIZVQOZCPVRVNWACVNVIVTVPVNVIVTVPOVNVINZVKATQZV
      PVTWBVJTIZVKNZAUAZVLVKNZAUAZWCVPWBWEWGOZAPZWFWHOVIVNWJVIVMWIAVMVIWIVMVIVK
      VLOZOVIWIOVIVKVLUBWKWIVIWKVKWGWDVKVLUCUDUEUFUGUHUIWEWGAUJUKVKATULVKAVFULU
      MAVHUNUOUPUQURVQCRUSUTVAACVFVBVSDBEVCVDVE $.
      $( [24-Dec-2001] $)

    $( The hypothesis defines the set of complete subspaces of Hilbert
       space (see ~ chsscm ).  A Hilbert subspace is closed iff it is complete.
       Remark 3.12(C) of [Beran] p. 107. $)
    chcmh $p |- CH = C $=
      ( cch chsscm cv csh wcel cn wf chli wbr wrex wi ccau wral wa wal df-ral
      ax-17 hbre1 hbim visset hlimcau imim1i weu wex 19.8a hlimeu sylib eupick
      ex df-rex exancom bitr syl5ib syl pm2.43a sylcom imim2i bi2.04 impexp
      3imtr4 19.21ai 19.20i sylbi anim2i abeq2i closedsub ssriv eqssi ) FBABCDE
      GDBFDHZIJZKVNCHZLZVPAHZMNZAVNOZPZCQRZSZVOVQVSSVRVNJZPZATZCTZSVNBJVNFJWBWG
      VOWBVPQJZWAPZCTWGWACQUAWIWFCWIWEAWHWAAWHAUBVQVTAVQAUBVSAVNUCUDUDVQWHVTPZP
      VQVSWDPZPWIWEWJWKVQWJVSVTWDVSWHVTVRVPAUECUEZUFUGVTVSWDVSVSAUHZVTWKPVSVSAU
      IWMVSAUJAVPWLUKULWMVSWDSAUIZWKVTWMWNWKVSWDAUMUNVTWDVSSAUIWNVSAVNUOWDVSAUP
      UQURUSUTVAVBWHVQVTVCVQVSWDVDVEVFVGVHVIWCDBEVJACVNVKVEVLVM $.
      $( [24-Dec-2001] $)
  $}

  ${
    $d x f h H $.  $d x f h g $.
    $( A Hilbert subspace is closed iff it is complete.  Remark 3.12(C) of
      [Beran] p. 107. $)
    ch2 $p |- ( H e. CH <-> ( H e. SH /\ A. f e. Cauchy (
      f : NN --> H -> E. x e. H f ~~>v x ) ) ) $=
      ( vh vg cch wcel cvv csh cn cv wf chli wbr wrex wi ccau wral wa elisset
      adantr wceq eleq1 feq3 rexeq1 imbi12d ralbidv anbi12d cab cbvabv chcmh
      elab2g pm5.21nii ) CFGCHGZCIGZJCBKZLZUPAKMNZACOZPZBQRZSZCFTUOUNVACITUADKZ
      IGZJVCUPLZURAVCOZPZBQRZSZVBDCFHVCCUBZVDUOVHVAVCCIUCVJVGUTBQVJVEUQVFUSVCCJ
      UPUDURAVCCUEUFUGUHAVIDUIBEVIEKZIGZJVKUPLZURAVKOZPZBQRZSDEVCVKUBZVDVLVHVPV
      CVKIUCVQVGVOBQVQVEVMVFVNVCVKJUPUDURAVCVKUEUFUGUHUJUKULUM $.
      $( [24-Dec-2001] $)
  $}

  ${
    $d x f H $.  $d x f F $.
    $( Completeness of a closed subspace of Hilbert space. $)
    chcompl $p |- ( H e. CH -> ( ( F e. Cauchy /\ F : NN --> H ) ->
                      E. x e. H F ~~>v x ) ) $=
      ( vf cch wcel cn cv wf chli wbr wrex wi ccau wral wa csh ch2 pm3.27bi
      wceq feq1 breq1 rexbidv imbi12d rcla4cv imp3a syl ) CEFZGCDHZIZUIAHZJKZAC
      LZMZDNOZBNFZGCBIZPBUKJKZACLZMUHCQFUOADCRSUOUPUQUSUNUQUSMDBNUIBTZUJUQUMUSG
      CUIBUAUTULURACUIBUKJUBUCUDUEUFUG $.
      $( [4-Oct-1999] $)
  $}

  ${
    $d x y f $.
    $( The unit Hilbert lattice element (which is all of Hilbert space) belongs
       to the Hilbert lattice.  Part of Proposition 1 of [Kalmbach] p. 65. $)
    helch $p |- H~ e. CH $=
      ( vf vx vy chil cch wcel csh cn cv wf chli wbr wa wi wal closedsub wss
      c0v cva co wral csm cc sh ssid ax-hv0cl pm3.2i hvaddclt rgen2a hvmulclt
      rgen2 mpbir2an visset hlimvec adantl gen2 ) DEFDGFZHDAIZJZURBIZKLZMUTDFZN
      ZBOAOBADPUQDDQZRDFZMUTCIZSTDFZCDUABDUAZUTVFUBTDFZCDUABUCUAZMBCDUDVDVEDUEU
      FUGVHVJVGBCDUTVFUHUIVIBCUCDUTVFUJUKUGULVCABVAVBUSUTURAUMBUMUNUOUPUL $.
      $( [6-Sep-1999] $)
  $}

  $( Hilbert space is a subspace of Hilbert space. $)
  helsh $p |- H~ e. SH $=
    ( chil helch chshi ) ABC $.
    $( [3-Jun-2004] $) $( [2-Jun-2004] $)

  $( Subspaces are subsets of Hilbert space. $)
  shsspwh $p |- SH (_ P~ H~ $=
    ( vx csh cuni cpw chil pwuni wceq wcel cv wss wral helsh shss rgen ssunieq
    mp2an pweq ax-mp sseqtr4 ) BBCZDZEDZBFETGZUBUAGEBHAIZEJZABKUCLUEABUDMNAEBOP
    ETQRS $.
    $( [26-Nov-2004] $) $( [24-Nov-2004] $)

  $( Closed subspaces are subsets of Hilbert space. $)
  chsspwh $p |- CH (_ P~ H~ $=
    ( cch csh chil cpw chsssh shsspwh sstri ) ABCDEFG $.
    $( [26-Nov-2004] $) $( [24-Nov-2004] $)

  ${
    $d x y f $.
    $( The zero subspace belongs to the set of closed subspaces of Hilbert
       space. $)
    hsn0elch $p |- { 0h } e. CH $=
      ( vf vx vy c0v csn cch wcel csh cn cv wf chli wbr wa wi wal closedsub
      chil wss cva co wral csm cc sh ax-hv0cl snssi ax-mp elisseti snid pm3.2i
      wceq opreq12 hvaddid2 syl6eq oprex elsnc sylibr elsn syl2anb rgen2a
      opreq2 hvmul0t sylan9eqr sylan2b rgen2 mpbir2an wb visset hlimuni eleq1d
      cxp fconst2 hlim0 breq1 mpbiri sylbi sylan mpbii gen2 ) DEZFGWAHGZIWAAJZK
      ZWCBJZLMZNZWEWAGZOZBPAPBAWAQWBWARSZDWAGZNWECJZTUAZWAGZCWAUBBWAUBZWEWLUCUA
      ZWAGZCWAUBBUDUBZNBCWAUEWJWKDRGWJUFDRUGUHDDRUFUIZUJZUKWOWRWNBCWAWEDULZWLDU
      LZWNWHWLWAGZXAXBNZWMDULWNXDWMDDTUADWEDWLDTUMDUFUNUOWMDWEWLTUPUQURBDUSCDUS
      ZUTVAWQBCUDWAWEUDGZXBWQXCXFXBNWPDULWQXBXFWPWEDUCUADWLDWEUCVBWEVCVDWPDWEWL
      UCUPUQURXEVEVFUKVGWIABWGWKWHWTWCDLMZWFWKWHVHWDXGWFNDWEWADWEWCWSBVIAVIVJVK
      WDWCIWAVLZULZXGIDWCWSVMXIXGXHDLMVNWCXHDLVOVPVQVRVSVTVG $.
      $( [14-Oct-1999] $)
  $}

  $( From any nonzero Hilbert space vector, construct a vector whose norm is
     1. $)
  norm1t $p |- ( ( A e. H~ /\ A =/= 0h ) ->
               ( normh ` ( ( 1 / ( normh ` A ) ) .h A ) ) = 1 ) $=
    ( chil wcel c0v wne wa c1 cno cfv cdiv co csm cabs cmul cc wceq norm-iiit
    cr cc0 rerecclt normclt adantr normne0t biimpar sylanc recnd pm3.26 cle
    wbr absidt clt 1re 0re lt01 ltlei divge0t mpanl12 normgt0t biimpa opreq1d
    axmulcom recidt eqtrd 3eqtrd ) ABCZADEZFZGAHIZJKZALKHIZVIMIZVHNKZVIVHNKZGVI
    OCZVEVJVLPVGVIAQVGVIVHRCZVHSEZVIRCZVGVHTVEVOVFAUAZUBZVEVPVFAUCUDZUEZUFZVEVF
    UGUEVGVKVIVHNVQSVIUHUIZVKVIPVGVIUJWAVOSVHUKUIZWCVGGRCSGUHUIVOWDFWCULSGUMULU
    NUOGVHUPUQVSVEVFWDAURUSUEUEUTVGVMVHVINKZGVNVHOCZVMWEPVGVIVHVAWBVEWFVFVEVHVR
    UFUBZUEWFVPWEGPVGVHVBWGVTUEVCVD $.
    $( [8-Feb-2006] $) $( [7-Feb-2006] $)

  ${
    $d x z H $.  $d y z H $.
    norm1ex.1 $e |- H e. SH $.
    $( A normalized vector exists in a subspace iff the subspace has a non-zero
       vector. $)
    norm1ex $p |- ( E. x e. H x =/= 0h <-> E. y e. H ( normh ` y ) = 1 ) $=
      ( vz cv c0v wne wrex cno cfv c1 wceq neeq1 cbvrexv cdiv co csm wcel wa
      fveq2 eqeq1d rcla4ev cc csh wi shmulcltOLD ax-mp cr cc0 rerecclt chil
      shel normclt syl adantr wb normne0t biimpar sylanc recnd pm3.26 norm1t
      sylan r19.23aiva wn norm-it necon3bbid ax1ne0 df-ne mpbi eqeq1 mtbiri
      syl5bi r19.22i sylib impbi bitr ) AFZGHZACIEFZGHZECIZBFZJKZLMZBCIZVTWBAEC
      VSWAGNOWCWGWBWGECLWAJKZPQZWARQZCSZWJJKZLMZWGWACSZWBTZWFWMBWJCWDWJMWEWLLWD
      WJJUAUBUCWIUDSZWNWKWOCUESWPWNTWKUFDWIWACUGUHWOWIWHUISZWHUJHZWIUISWOWHUKWN
      WQWBWNWAULSZWQWACDUMZWAUNUOUPWNWRWBWNWSWRWBUQWTWAURUOUSUTVAWNWBVBUTWSWBWM
      WNWAVCWTVDUTVEWGWDGHZBCIWCWFXABCWDCSZWEUJMZVFXAWFXBXCWDGXBWDULSXCWDGMUQWD
      CDUMWDVGUOVHWFXCLUJMZLUJHXDVFVILUJVJVKWELUJVLVMVNVOXAWBBECWDWAGNOVPVQVR
      $.
      $( [9-Apr-2006] $) $( [9-Apr-2006] $)
  $}

  ${
    $d x z $.  $d y z $.
    $( A normalized vector can exist only iff the Hilbert space has a non-zero
       vector. $)
    norm1hext $p |- ( E. x e. H~ x =/= 0h <-> E. y e. H~ ( normh ` y ) = 1 ) $=
      ( chil helsh norm1ex ) ABCDE $.
      $( [2-Feb-2006] $) $( [21-Jan-2006] $)
  $}

