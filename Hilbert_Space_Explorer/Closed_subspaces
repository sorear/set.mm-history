$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Closed subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x f h $.
    $( Define the set of closed subspaces of a Hilbert space.  A closed
       subspace is one in which the limit of every convergent sequence in the
       subspace belongs to the subspace.  For its membership relation, see
       ~ closedsub .  From Definition of [Beran] p. 107.  Alternate definitions
       are given by ~ chcmhi and ~ dfch2 . $)
    df-ch $a |- CH = { h | ( h e. SH /\ A. f A. x ( ( f : NN --> h /\
        f ~~>v x ) -> x e. h ) ) } $.
  $}

  ${
    $d x f h H $.
    $( Closed subspace ` H ` of a Hilbert space.  Definition of [Beran]
       p. 107. $)
    closedsub $p |- ( H e. CH <-> ( H e. SH /\ A. f A. x ( ( f : NN --> H /\
        f ~~>v x ) -> x e. H ) ) ) $=
      ( vh cch wcel cvv csh cn cv wf chli wbr wa wi wal elisset adantr wceq
      eleq1 feq3 anbi1d eleq2 imbi12d 2albidv anbi12d df-ch elab2g pm5.21nii )
      CEFCGFZCHFZICBJZKZULAJZLMZNZUNCFZOZAPBPZNZCEQUKUJUSCHQRDJZHFZIVAULKZUONZU
      NVAFZOZAPBPZNUTDCEGVACSZVBUKVGUSVACHTVHVFURBAVHVDUPVEUQVHVCUMUOVACIULUAUB
      VACUNUCUDUEUFABDUGUHUI $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x f h $.
    $( Closed subspaces are subspaces in a Hilbert space. $)
    chsssh $p |- CH C_ SH $=
      ( vh vf vx cch cv csh wcel cn wf chli wbr wa wi wal cab df-ch ssab2
      eqsstri ) DAEZFGHSBEZITCEZJKLUASGMCNBNZLAOFCBAPUBAFQR $.
      $( [29-May-1999] $)
  $}

  $( The set of closed subspaces of a Hilbert space exists (is a set). $)
  chex $p |- CH e. _V $=
    ( cch csh shex chsssh ssexi ) ABCDE $.
    $( [23-Oct-1999] $)

  $( A closed subspace is a subspace. $)
  chsh $p |- ( H e. CH -> H e. SH ) $=
    ( cch csh chsssh sseli ) BCADE $.
    $( [19-Oct-1999] $)

  ${
    chshi.1 $e |- H e. CH $.
    $( A closed subspace is a subspace. $)
    chshii $p |- H e. SH $=
      ( cch wcel csh chsh ax-mp ) ACDAEDBAFG $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d x y H $.
    $( The zero vector belongs to any closed subspace of a Hilbert space. $)
    ch0 $p |- ( H e. CH -> 0h e. H ) $=
      ( cch wcel csh c0v chsh sh0 syl ) ABCADCEACAFAGH $.
      $( [24-Aug-1999] $)

    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chss $p |- ( H e. CH -> H C_ ~H ) $=
      ( cch wcel csh chil wss chsh shss syl ) ABCADCAEFAGAHI $.
      $( [24-Aug-1999] $)

  $}

  $( A member of a closed subspace of a Hilbert space is a vector. $)
  chel $p |- ( ( H e. CH /\ A e. H ) -> A e. ~H ) $=
    ( cch wcel chil chss sseld imp ) BCDZABDAEDIBEABFGH $.
    $( [15-Dec-2004] $)

  ${
    chssi.1 $e |- H e. CH $.
    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chssii $p |- H C_ ~H $=
      ( chshii shssii ) AABCD $.
      $( [6-Oct-1999] $)

    $( A member of a closed subspace of a Hilbert space is a vector. $)
    cheli $p |- ( A e. H -> A e. ~H ) $=
      ( chil chssii sseli ) BDABCEF $.
      $( [6-Oct-1999] $)

    ${
      cheli.1 $e |- A e. H $.
      $( A member of a closed subspace of a Hilbert space is a vector. $)
      chelii $p |- A e. ~H $=
        ( chil chssii sselii ) BEABCFDG $.
        $( [6-Oct-1999] $)
    $}
  $}

  ${
    $d x f F $.  $d x f H $.  $d x A $.
    chlim.1 $e |- A e. _V $.
    $( The limit property of a closed subspace of a Hilbert space. $)
    chlimi $p |- ( ( H e. CH /\ F : NN --> H /\ F ~~>v A ) -> A e. H ) $=
      ( vf vx cch wcel cn wf chli wbr cv wa wi wal csh closedsub simprbi cvv
      nnex fex mpan2 adantr wceq feq1 breq1 anbi12d imbi1d albidv cla4gv breq2
      anbi2d eleq1 imbi12d cla4v syl6 syl pm2.43b 3impib ) CGHZICBJZBAKLZACHZVA
      ICEMZJZVEFMZKLZNZVGCHZOZFPZEPZVBVCNZVDOZVACQHVMFECRSVMVNVDVNBTHZVMVOOVBVP
      VCVBITHVPUAICTBUBUCUDVPVMVBBVGKLZNZVJOZFPZVOVLVTEBTVEBUEZVKVSFWAVIVRVJWAV
      FVBVHVQICVEBUFVEBVGKUGUHUIUJUKVSVOFADVGAUEZVRVNVJVDWBVQVCVBVGABKULUMVGACU
      NUOUPUQURUSURUT $.
      $( [14-Apr-2008] $) $( [14-Sep-1999] $)
  $}

  ${
    $d x y z $.
    $( The zero sequence in Hilbert space converges to the zero vector. $)
    hlim0 $p |- ( NN X. { 0h } ) ~~>v 0h $=
      ( vx vy vz cn c0v csn cxp chli wbr chil wf wcel wa cc0 cv clt cle cfv
      cmv co cno wi wral wrex cr nnex snex xpex ax-hv0cl elisseti hlimi wss
      fconst snssi ax-mp fss mp2an pm3.2i c1 fvconst2 opreq1d wceq hvsubid
      syl6eq fveq2d norm0 breq1d biimprd a1d com3r r19.21aiv 1nn jctil breq1
      imbi1d ralbidv rcla4ev syl a1i rgen mpbir2an ) DEFZGZEHIDJWCKZEJLZMNAOZPI
      ZBOZCOZQIZWIWCRZESTZUARZWFPIZUBZCDUCZBDUDZUBZAUEUCABCEWCDWBUFEUGUHEJUIUJZ
      UKWDWEDWBWCKWBJULZWDDEWSUMWEWTUIEJUNUODWBJWCUPUQUIURWRAUEWRWFUELWGUSDLZUS
      WIQIZWNUBZCDUCZMWQWGXDXAWGXCCDWIDLZXBWGWNXEWGWNUBXBXEWNWGXEWMNWFPXEWMEUAR
      NXEWLEUAXEWLEESTZEXEWKEESDEWIWSUTVAWEXFEVBUIEVCUOVDVEVFVDVGVHVIVJVKVLVMWP
      XDBUSDWHUSVBZWOXCCDXGWJXBWNWHUSWIQVNVOVPVQVRVSVTWA $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x y z w v A $.  $d x y z w v F $.
    hlimcau.1 $e |- A e. _V $.
    hlimcau.2 $e |- F e. _V $.
    ${
      hlimcaui.4 $e |- F ~~>v A $.
      $( If a sequence in Hilbert space subset converges to a limit, it is a
         Cauchy sequence. $)
      hlimcauii $p |- F e. Cauchy $=
        ( vx vy vz vw vv ccau wcel cn chil wf cc0 cv clt wbr cle wa cfv cmv co
        cno wi wral wrex cr hcau chli hlimseqi ax-mp c2 cdiv rehalfcl adantr
        cif wceq breq2 opreq1 breq2d imbi12d 0re elimel 2re 2pos divgt0i2i
        dedth imp imbi2d rexralbidv hlimveci hlim2 mpbii mp2an vtoclri sylc
        prth ffvelrni jctil wb normsub breq1d anbi2d adantl anim1i ancoms an4s
        norm3lemt syl sylbird sylan2 syl9r ralimdva raaanv fveq2 opreq1d
        fveq2d cbvralv anbi2i anidm 3bitri syl5ibr reximdv mpd ex rgen
        mpbir2an ) BKLMNBOZPFQZRSZGQZHQZTSZXMIQZTSZUAZXNBUBZXPBUBZUCUDUEUBXKRSZ
        UFZIMUGZHMUGZGMUHZUFZFUIUGFGHIBUJBAUKSZXJEABDCULUMZYFFUIXKUILZXLYEYIXLU
        AZXOXSAUCUDZUEUBZXKUNUOUDZRSZUFZHMUGZGMUHZYEYJYMUILZPYMRSZYQYIYRXLXKUPU
        QYIXLYSYIXLYSUFPYIXKPURZRSZPYTUNUOUDZRSZUFXKPXKYTUSZXLUUAYSUUCXKYTPRUTU
        UDYMUUBPRXKYTUNUOVAVBVCYTUNXKPUIVDVEVFVGVHVIVJPJQZRSZXOYLUUERSZUFZHMUGG
        MUHZUFZYSYQUFJYMUIUUEYMUSZUUFYSUUIYQUUEYMPRUTUUKUUHYOGHMMUUKUUGYNXOUUEY
        MYLRUTVKVLVCXJANLZUUJJUIUGZYHYGUULEABDCVMUMZXJUULUAYGUUMEJGHABVNVOVPVQV
        RYIYQYEUFXLYIYPYDGMYIYOXQXTAUCUDZUEUBZYMRSZUFZUAZIMUGZHMUGZYDYPYIUUTYCH
        MYIXNMLZUAZUUSYBIMUUSXRYNUUQUAZUVCXPMLZUAYAXOYNXQUUQVSUVEUVCUULXTNLZUAZ
        UVDYAUFUVEUVFUULMNXPBYHVTUUNWAUVCUVGUAZUVDYNAXTUCUDUEUBZYMRSZUAZYAUVGUV
        KUVDWBUVCUVGUVJUUQYNUVGUVIUUPYMRAXTWCWDWEWFUVHXSNLZUVFUAZUULYIUAZUAZUVK
        YAUFUVGUVCUVOUULYIUVFUVBUVOUVFUVBUAZUVNUVOUVPUVMUVNUVBUVFUVMUVBUVLUVFMN
        XNBYHVTWGWHWGWHWIWHXSXTAXKWJWKWLWMWNWOWOUVAYPUURIMUGZUAYPYPUAYPYOUURHIM
        WPUVQYPYPUURYOIHMXPXNUSZXQXOUUQYNXPXNXMTUTUVRUUPYLYMRUVRUUOYKUEUVRXTXSA
        UCXPXNBWQWRWSWDVCWTXAYPXBXCXDXEUQXFXGXHXI $.
        $( [17-Aug-1999] $)
    $}

    $( If a sequence in Hilbert space subset converges to a limit, it is a
       Cauchy sequence. $)
    hlimcaui $p |- ( F ~~>v A -> F e. Cauchy ) $=
      ( chli wbr ccau wcel cn c0v csn cxp cif eleq1 chil ax-hv0cl elisseti
      ifex nnex snex xpex breq1 breq2 hlim0 elimhyp2v hlimcauii dedth ) BAEFZBG
      HUHBIJKZLZMZGHBUJBUKGNUHAJMZUKUHAJCJOPQRUHBUJDIUISJTUARUHUKAEFUKULEFUJJEF
      UKJEFBAUJJBUKAEUBAULUKEUCUJUKJEUBJULUKEUCUDUEUFUG $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x y z w v A $.  $d x y z w v B $.  $d x y z w v F $.
    hlimuni.1 $e |- A e. _V $.
    hlimuni.2 $e |- B e. _V $.
    hlimuni.3 $e |- F e. _V $.
    ${
      hlimunii.3 $e |- ( F ~~>v A /\ F ~~>v B ) $.
      $( A Hilbert space sequence converges to at most one limit. $)
      hlimuniii $p |- A = B $=
        ( vy vz vw vx cmv co c0v wceq cc0 cno cfv clt wbr wn cv cle wa cn wrex
        wral nn2ge rgen2a c2 cdiv wi chli chil wcel simpli hlimveci ax-mp
        simpri hvsubcli normcli 2re 2pos divgt0i2i cr 2ne0 redivcli hlimconvi
        breq2 imbi2d rexralbidv imbi12d rcla4v mp2 jca r19.26 con3 ltnri wf
        hlimseqi ffvelrni normsub mpan2 syl breq1d anbi1d norm3lemt mpanl12
        sylbid mtoi syl5com prth syl5 ralimia sylbir reximi reeanv ralnex
        rexbii rexnal bitri 3imtr3i 3syl mt2 wne wb normgt0 necon1bbii mpbi
        hvsubeq0i ) ABLMZNOZABOPXKQRZSTZUAXLXNHUBZIUBZUCTZJUBZXPUCTZUDZIUEUFZJU
        EUGZHUEUGZYAHJUEIXOXRUHUIXNPXMUJUKMZSTZXQXPCRZALMQRZYDSTZULZIUEUGZHUEUF
        ZXSYFBLMQRZYDSTZULZIUEUGZJUEUFZUDZYCUAZXMUJXKABCAUMTZAUNUOZYSCBUMTZGUPZ
        ACFDUQURZUUABUNUOZYSUUAGUSZBCFEUQURZUTZVAZVBVCVDYEYKYPYDVEUOZPKUBZSTZXQ
        YGUUJSTZULZIUEUGHUEUFZULZKVEUGZYEYKULZXMUJUUHVBVFVGZYSUUPUUBKHIACFDVHUR
        UUOUUQKYDVEUUJYDOZUUKYEUUNYKUUJYDPSVIZUUSUUMYIHIUEUEUUSUULYHXQUUJYDYGSV
        IVJVKVLVMVNUUIUUKXSYLUUJSTZULZIUEUGJUEUFZULZKVEUGZYEYPULZUURUUAUVEUUEKJ
        IBCFEVHURUVDUVFKYDVEUUSUUKYEUVCYPUUTUUSUVBYNJIUEUEUUSUVAYMXSUUJYDYLSVIV
        JVKVLVMVNVOYJYOUDZJUEUFZHUEUFXTUAZIUEUGZJUEUFZHUEUFZYQYRUVHUVKHUEUVGUVJ
        JUEUVGYIYNUDZIUEUGUVJYIYNIUEVPUVMUVIIUEXPUEUOZXTYHYMUDZULZUVIUVMUVPUVOU
        AUVIUVNXTUVOVQUVNUVOXMXMSTZXMUUHVRUVNUVOAYFLMQRZYDSTZYMUDZUVQUVNYHUVSYM
        UVNYGUVRYDSUVNYFUNUOZYGUVROZUEUNXPCYSUEUNCVSUUBACFDVTURWAZUWAYTUWBUUCYF
        AWBWCWDWEWFUVNUWAUVTUVQULZUWCUWAXMVEUOZUWDUUHYTUUDUWAUWEUDUWDUUCUUFABYF
        XMWGWHWCWDWIWJWKXQYHXSYMWLWMWNWOWPWPYJYOHJUEUEWQUVLYBUAZHUEUFYRUVKUWFHU
        EUVKYAUAZJUEUFUWFUVJUWGJUEXTIUEWRWSYAJUEWTXAWSYBHUEWTXAXBXCXDXNXKNXKUNU
        OXKNXEXNXFUUGXKXGURXHXIABUUCUUFXJXI $.
        $( [19-Aug-1999] $)
    $}

    $( A Hilbert space sequence converges to at most one limit. $)
    hlimunii $p |- ( ( F ~~>v A /\ F ~~>v B ) -> A = B ) $=
      ( chli wbr wa wceq c0v cif eqeq1 eqeq2 cn csn cxp chil ax-hv0cl elisseti
      ifex nnex snex xpex breq2 anbi1d anbi2d breq1 anbi12d hlim0 pm3.2i
      elimhyp3v hlimuniii dedth2v ) CAGHZCBGHZIZABJUQAKLZBJURUQBKLZJABKKAURBMBU
      SURNURUSUQCOKPZQZLZUQAKDKRSTZUAUQBKEVCUAUQCVAFOUTUBKUCUDUAUQCURGHZUPIVDCU
      SGHZIVBURGHZVBUSGHZIVAKGHZVHIVAURGHZVHIVIVAUSGHZIABCKKVAAURJUOVDUPAURCGUE
      UFBUSJUPVEVDBUSCGUEUGCVBJVDVFVEVGCVBURGUHCVBUSGUHUIKURJVHVIVHKURVAGUEUFKU
      SJVHVJVIKUSVAGUEUGVAVBJVIVFVJVGVAVBURGUHVAVBUSGUHUIVHVHUJUJUKULUMUN $.
      $( [19-Aug-1999] $)
  $}

  ${
    $d x y F $.  $d x y H $.
    hlimreu.1 $e |- F e. _V $.
    $( The limit of a Hilbert space sequence is unique. $)
    hlimreui $p |- ( E. x e. H F ~~>v x <-> E! x e. H F ~~>v x ) $=
      ( vy cv chli wbr wrex wreu wa wceq wi wral wcel visset hlimunii a1i
      rgen2a jctr breq2 reu4 sylibr reurex impbii ) BAFZGHZACIZUGACJZUHUHUGBEFZ
      GHZKUFUJLMZECNACNZKUIUHUMULAECULUFCOUJCOKUFUJBAPEPDQRSTUGUKAECUFUJBGUAUBU
      CUGACUDUE $.
      $( [19-Aug-1999] $)
  $}

  ${
    $d x y F $.  $d x y H $.
    hlimeu.1 $e |- F e. _V $.
    $( The limit of a Hilbert space sequence is unique. $)
    hlimeui $p |- ( E. x F ~~>v x <-> E! x F ~~>v x ) $=
      ( vy cv chli wbr wex weu wa wceq wi wal visset hlimunii gen2 jctr breq2
      eu4 sylibr euex impbii ) BAEZFGZAHZUDAIZUEUEUDBDEZFGZJUCUGKLZDMAMZJUFUEUJ
      UIADUCUGBANDNCOPQUDUHADUCUGBFRSTUDAUAUB $.
      $( [19-Aug-1999] $)
  $}

  ${
    $d x f h $.  $d h C $.
    cmh.1 $e |- C = { h | ( h e. SH /\ A. f e. Cauchy (
      f : NN --> h -> E. x e. h f ~~>v x ) ) } $.
    $( The hypothesis defines the set of complete subspaces of Hilbert space.
       A complete subspace is one in which every Cauchy sequence of vectors in
       the subspace converges to a member of the subspace (Definition of
       complete subspace in [Beran] p. 96).  Any closed subspace of a Hilbert
       space is complete.  Part of Remark 3.12 of [Beran] p. 107. $)
    chsscmi $p |- CH C_ C $=
      ( cch cv csh wcel cn wf chli wbr wa wi wal wrex ccau wral chil wex
      impexp ancr adantld imim2i sylbi com12 alimdv impcom exim syl df-rex
      3imtr4g ax-hcompl syl5 ex com23 alimi df-ral sylibr anim2i closedsub
      abeq2i 3imtr4i ssriv ) DFBDGZHIZJVFCGZKZVHAGZLMZNVJVFIZOZAPZCPZNVGVIVKAVF
      QZOZCRSZNZVFFIVFBIVOVRVGVOVHRIZVQOZCPVRVNWACVNVIVTVPVNVIVTVPOVNVINZVKATQZ
      VPVTWBVJTIZVKNZAUAZVLVKNZAUAZWCVPWBWEWGOZAPZWFWHOVIVNWJVIVMWIAVMVIWIVMVIV
      KVLOZOVIWIOVIVKVLUBWKWIVIWKVKWGWDVKVLUCUDUEUFUGUHUIWEWGAUJUKVKATULVKAVFUL
      UMAVHUNUOUPUQURVQCRUSUTVAACVFVBVSDBEVCVDVE $.
      $( [24-Dec-2001] $)

    $( The hypothesis defines the set of complete subspaces of Hilbert space
       (see ~ chsscmi ).  A Hilbert subspace is closed iff it is complete.
       Remark 3.12(C) of [Beran] p. 107. $)
    chcmhi $p |- CH = C $=
      ( cch chsscmi cv csh wcel cn wf chli wbr wrex wi ccau wral wa wal df-ral
      ax-17 hbre1 hbim visset hlimcaui imim1i weu wex 19.8a hlimeui sylib
      eupick ex df-rex exancom bitri syl5ib syl pm2.43a sylcom imim2i bi2.04
      impexp 3imtr4i 19.21ai alimi sylbi anim2i abeq2i closedsub ssriv eqssi )
      FBABCDEGDBFDHZIJZKVNCHZLZVPAHZMNZAVNOZPZCQRZSZVOVQVSSVRVNJZPZATZCTZSVNBJV
      NFJWBWGVOWBVPQJZWAPZCTWGWACQUAWIWFCWIWEAWHWAAWHAUBVQVTAVQAUBVSAVNUCUDUDVQ
      WHVTPZPVQVSWDPZPWIWEWJWKVQWJVSVTWDVSWHVTVRVPAUECUEZUFUGVTVSWDVSVSAUHZVTWK
      PVSVSAUIWMVSAUJAVPWLUKULWMVSWDSAUIZWKVTWMWNWKVSWDAUMUNVTWDVSSAUIWNVSAVNUO
      WDVSAUPUQURUSUTVAVBWHVQVTVCVQVSWDVDVEVFVGVHVIWCDBEVJACVNVKVEVLVM $.
      $( [24-Dec-2001] $)
  $}

  ${
    $d x f h H $.  $d x f h g $.
    $( A Hilbert subspace is closed iff it is complete.  Remark 3.12(C) of
       [Beran] p. 107. $)
    ch2 $p |- ( H e. CH <-> ( H e. SH /\ A. f e. Cauchy (
      f : NN --> H -> E. x e. H f ~~>v x ) ) ) $=
      ( vh vg cch wcel cvv csh cn cv wf chli wbr wrex wi ccau wral wa elisset
      adantr wceq eleq1 feq3 rexeq imbi12d ralbidv anbi12d cab cbvabv chcmhi
      elab2g pm5.21nii ) CFGCHGZCIGZJCBKZLZUPAKMNZACOZPZBQRZSZCFTUOUNVACITUADKZ
      IGZJVCUPLZURAVCOZPZBQRZSZVBDCFHVCCUBZVDUOVHVAVCCIUCVJVGUTBQVJVEUQVFUSVCCJ
      UPUDURAVCCUEUFUGUHAVIDUIBEVIEKZIGZJVKUPLZURAVKOZPZBQRZSDEVCVKUBZVDVLVHVPV
      CVKIUCVQVGVOBQVQVEVMVFVNVCVKJUPUDURAVCVKUEUFUGUHUJUKULUM $.
      $( [24-Dec-2001] $)
  $}

  ${
    $d x f H $.  $d x f F $.
    $( Completeness of a closed subspace of Hilbert space. $)
    chcompl $p |- ( ( H e. CH /\ F e. Cauchy /\ F : NN --> H ) ->
                      E. x e. H F ~~>v x ) $=
      ( vf cch wcel ccau cn wf cv chli wbr wrex wi wral csh ch2 simprbi wceq
      feq1 breq1 rexbidv imbi12d rcla4cv syl 3imp ) CEFZBGFZHCBIZBAJZKLZACMZUGH
      CDJZIZUMUJKLZACMZNZDGOZUHUIULNZNUGCPFURADCQRUQUSDBGUMBSZUNUIUPULHCUMBTUTU
      OUKACUMBUJKUAUBUCUDUEUF $.
      $( [13-Apr-2008] $) $( [4-Oct-1999] $)
  $}

  ${
    $d x y f $.
    $( The unit Hilbert lattice element (which is all of Hilbert space) belongs
       to the Hilbert lattice.  Part of Proposition 1 of [Kalmbach] p. 65. $)
    helch $p |- ~H e. CH $=
      ( vf vx vy chil cch wcel csh cn cv wf chli wbr wa wi wal closedsub wss
      c0v cva co wral csm cc sh ssid ax-hv0cl pm3.2i hvaddcl rgen2a hvmulcl
      rgen2 mpbir2an visset hlimveci adantl gen2 ) DEFDGFZHDAIZJZURBIZKLZMUTDFZ
      NZBOAOBADPUQDDQZRDFZMUTCIZSTDFZCDUABDUAZUTVFUBTDFZCDUABUCUAZMBCDUDVDVEDUE
      UFUGVHVJVGBCDUTVFUHUIVIBCUCDUTVFUJUKUGULVCABVAVBUSUTURAUMBUMUNUOUPUL $.
      $( [6-Sep-1999] $)
  $}

  $( Hilbert space is a subspace of Hilbert space. $)
  helsh $p |- ~H e. SH $=
    ( chil helch chshii ) ABC $.
    $( [2-Jun-2004] $)

  $( Subspaces are subsets of Hilbert space. $)
  shsspwh $p |- SH C_ ~P ~H $=
    ( vx csh cuni cpw chil pwuni wceq wcel cv wss wral helsh shss rgen ssunieq
    mp2an pweq ax-mp sseqtr4i ) BBCZDZEDZBFETGZUBUAGEBHAIZEJZABKUCLUEABUDMNAEBO
    PETQRS $.
    $( [24-Nov-2004] $)

  $( Closed subspaces are subsets of Hilbert space. $)
  chsspwh $p |- CH C_ ~P ~H $=
    ( cch csh chil cpw chsssh shsspwh sstri ) ABCDEFG $.
    $( [24-Nov-2004] $)

  ${
    $d x y f $.
    $( The zero subspace belongs to the set of closed subspaces of Hilbert
       space. $)
    hsn0elch $p |- { 0h } e. CH $=
      ( vf vx vy c0v csn cch wcel csh cn cv wf chli wbr wa wi wal closedsub
      chil wss cva co wral csm cc sh ax-hv0cl snssi ax-mp elisseti snid pm3.2i
      wceq elsn opreq12 hvaddid2i syl6eq oprex elsnc sylibr syl2anb rgen2a
      opreq2 hvmul0 sylan9eqr sylan2b rgen2 mpbir2an wb cxp fconst2 hlim0
      breq1 mpbiri sylbi visset hlimunii eleq1d sylan mpbii gen2 ) DEZFGWAHGZIW
      AAJZKZWCBJZLMZNZWEWAGZOZBPAPBAWAQWBWARSZDWAGZNWECJZTUAZWAGZCWAUBBWAUBZWEW
      LUCUAZWAGZCWAUBBUDUBZNBCWAUEWJWKDRGWJUFDRUGUHDDRUFUIZUJZUKWOWRWNBCWAWHWED
      ULZWLDULZWNWLWAGZBDUMCDUMZXAXBNZWMDULWNXEWMDDTUADWEDWLDTUNDUFUOUPWMDWEWLT
      UQURUSUTVAWQBCUDWAXCWEUDGZXBWQXDXFXBNWPDULWQXBXFWPWEDUCUADWLDWEUCVBWEVCVD
      WPDWEWLUCUQURUSVEVFUKVGWIABWGWKWHWTWDWCDLMZWFWKWHVHWDWCIWAVIZULZXGIDWCWSV
      JXIXGXHDLMVKWCXHDLVLVMVNXGWFNDWEWADWEWCWSBVOAVOVPVQVRVSVTVG $.
      $( [14-Oct-1999] $)
  $}

  $( From any nonzero Hilbert space vector, construct a vector whose norm is
     1. $)
  norm1 $p |- ( ( A e. ~H /\ A =/= 0h ) ->
               ( normh ` ( ( 1 / ( normh ` A ) ) .h A ) ) = 1 ) $=
    ( chil wcel c0v wne wa c1 cno cfv cdiv co csm cabs cmul cc wceq cr cc0
    normcl adantr normne0 biimpar rereccl syl11anc recnd simpl norm-iii cle
    wbr clt normgt0 biimpa 1re 0re lt01 ltleii divge0 mpanl12 absid opreq1d
    mulcom recid eqtrd 3eqtrd ) ABCZADEZFZGAHIZJKZALKHIZVIMIZVHNKZVIVHNKZGVGVIO
    CZVEVJVLPVGVIVGVHQCZVHREZVIQCZVEVOVFASZTZVEVPVFAUAUBZVHUCUDZUEZVEVFUFVIAUGU
    DVGVKVIVHNVGVQRVIUHUIZVKVIPWAVGVORVHUJUIZWCVSVEVFWDAUKULGQCRGUHUIVOWDFWCUMR
    GUNUMUOUPGVHUQURUDVIUSUDUTVGVMVHVINKZGVGVNVHOCZVMWEPWBVEWFVFVEVHVRUETZVIVHV
    AUDVGWFVPWEGPWGVTVHVBUDVCVD $.
    $( [7-Feb-2006] $)

  ${
    $d x z H $.  $d y z H $.
    norm1ex.1 $e |- H e. SH $.
    $( A normalized vector exists in a subspace iff the subspace has a nonzero
       vector. $)
    norm1exi $p |- ( E. x e. H x =/= 0h <-> E. y e. H ( normh ` y ) = 1 ) $=
      ( vz cv c0v wne wrex cno cfv c1 wceq neeq1 cbvrexv wcel wa cdiv co csm
      cc cr cc0 chil sheli normcl syl adantr wb normne0 biimpar rereccl
      syl11anc recnd simpl csh wi shmulclOLD ax-mp norm1 sylan fveq2 eqeq1d
      rcla4ev r19.23aiva wn norm-i necon3bbid ax1ne0 df-ne mpbi eqeq1 mtbiri
      syl5bi reximia sylib impbii bitri ) AFZGHZACIEFZGHZECIZBFZJKZLMZBCIZVTWBA
      ECVSWAGNOWCWGWBWGECWACPZWBQZLWAJKZRSZWATSZCPZWLJKZLMZWGWIWKUAPZWHWMWIWKWI
      WJUBPZWJUCHZWKUBPWHWQWBWHWAUDPZWQWACDUEZWAUFUGUHWHWRWBWHWSWRWBUIWTWAUJUGU
      KWJULUMUNWHWBUOCUPPWPWHQWMUQDWKWACURUSUMWHWSWBWOWTWAUTVAWFWOBWLCWDWLMWEWN
      LWDWLJVBVCVDUMVEWGWDGHZBCIWCWFXABCWDCPZWEUCMZVFXAWFXBXCWDGXBWDUDPXCWDGMUI
      WDCDUEWDVGUGVHWFXCLUCMZLUCHXDVFVILUCVJVKWELUCVLVMVNVOXAWBBECWDWAGNOVPVQVR
      $.
      $( [9-Apr-2006] $)
  $}

  ${
    $d x z $.  $d y z $.
    $( A normalized vector can exist only iff the Hilbert space has a nonzero
       vector. $)
    norm1hex $p |- ( E. x e. ~H x =/= 0h <-> E. y e. ~H ( normh ` y ) = 1 ) $=
      ( chil helsh norm1exi ) ABCDE $.
      $( [21-Jan-2006] $)
  $}

