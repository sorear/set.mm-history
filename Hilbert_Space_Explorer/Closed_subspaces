$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Closed subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of closed subspaces of a Hilbert space.  A closed subspace
     is one in which the limit of every convergent sequence in the subspace
     belongs to the subspace.  For its membership relation, see ~ isch .  From
     Definition of [Beran] p. 107.  Alternate definitions are given by ~ isch2
     and ~ isch3 . $)
  df-ch $a |- CH = { h e. SH | ( ~~>v " ( h ^m NN ) ) C_ h } $.

  ${
    $d h H $.
    $( Closed subspace ` H ` of a Hilbert space.  (Contributed by Mario
       Carneiro, 23-Dec-2013.) $)
    isch $p |- ( H e. CH <-> ( H e. SH /\ ( ~~>v " ( H ^m NN ) ) C_ H ) ) $=
      ( vh chli cv cn cmap co cima wss csh wceq opreq1 imaeq2d id sseq12d df-ch
      cch elrab2 ) CBDZEFGZHZSICAEFGZHZAIBAJQSAKZUAUCSAUDTUBCSAEFLMUDNOBPR $.
      $( [23-Dec-2013] $) $( [17-Aug-1999] $)
  $}

  ${
    $d x f H $.
    $( Closed subspace ` H ` of a Hilbert space.  Definition of [Beran]
       p. 107. $)
    isch2 $p |- ( H e. CH <-> ( H e. SH /\ A. f A. x ( ( f : NN --> H /\
        f ~~>v x ) -> x e. H ) ) ) $=
      ( cch wcel csh chli cn cmap co cima wss wa cv wf wbr wi wal isch cvv nnex
      wex 19.23v elima2 imbi1i bitr4i albii alcom dfss2 3bitr4i wb elmapg mpan2
      vex anbi1d imbi1d 2albidv syl5bbr pm5.32i bitri ) CDECFEZGCHIJZKZCLZMVAHC
      BNZOZVEANZGPZMZVGCEZQZARBRZMCSVAVDVLVDVEVBEZVHMZVJQZARBRZVAVLVOBRZARVGVCE
      ZVJQZARVPVDVQVSAVQVNBUBZVJQVSVNVJBUCVRVTVJBVGGVBAUNUDUEUFUGVOBAUHAVCCUIUJ
      VAVOVKBAVAVNVIVJVAVMVFVHVAHTEVMVFUKUACHVEFTULUMUOUPUQURUSUT $.
      $( [23-Dec-2013] $) $( [17-Aug-1999] $)
  $}

  $( A closed subspace is a subspace. $)
  chsh $p |- ( H e. CH -> H e. SH ) $=
    ( cch wcel csh chli cn cmap co cima wss isch simplbi ) ABCADCEAFGHIAJAKL $.
    $( [23-Dec-2013] $) $( [19-Oct-1999] $)

  $( Closed subspaces are subspaces in a Hilbert space. $)
  chsssh $p |- CH C_ SH $=
    ( vx cch csh cv chsh ssriv ) ABCADEF $.
    $( [23-Dec-2013] $) $( [29-May-1999] $)

  $( The set of closed subspaces of a Hilbert space exists (is a set). $)
  chex $p |- CH e. _V $=
    ( cch csh shex chsssh ssexi ) ABCDE $.
    $( [23-Oct-1999] $)

  ${
    chshi.1 $e |- H e. CH $.
    $( A closed subspace is a subspace. $)
    chshii $p |- H e. SH $=
      ( cch wcel csh chsh ax-mp ) ACDAEDBAFG $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d x y H $.
    $( The zero vector belongs to any closed subspace of a Hilbert space. $)
    ch0 $p |- ( H e. CH -> 0h e. H ) $=
      ( cch wcel csh c0v chsh sh0 syl ) ABCADCEACAFAGH $.
      $( [24-Aug-1999] $)

    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chss $p |- ( H e. CH -> H C_ ~H ) $=
      ( cch wcel csh chil wss chsh shss syl ) ABCADCAEFAGAHI $.
      $( [24-Aug-1999] $)

  $}

  $( A member of a closed subspace of a Hilbert space is a vector. $)
  chel $p |- ( ( H e. CH /\ A e. H ) -> A e. ~H ) $=
    ( cch wcel chil chss sseld imp ) BCDZABDAEDIBEABFGH $.
    $( [15-Dec-2004] $)

  ${
    chssi.1 $e |- H e. CH $.
    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chssii $p |- H C_ ~H $=
      ( chshii shssii ) AABCD $.
      $( [6-Oct-1999] $)

    $( A member of a closed subspace of a Hilbert space is a vector. $)
    cheli $p |- ( A e. H -> A e. ~H ) $=
      ( chil chssii sseli ) BDABCEF $.
      $( [6-Oct-1999] $)

    ${
      cheli.1 $e |- A e. H $.
      $( A member of a closed subspace of a Hilbert space is a vector. $)
      chelii $p |- A e. ~H $=
        ( chil chssii sselii ) BEABCFDG $.
        $( [6-Oct-1999] $)
    $}
  $}

  ${
    $d x f F $.  $d x f H $.  $d x A $.
    chlim.1 $e |- A e. _V $.
    $( The limit property of a closed subspace of a Hilbert space. $)
    chlimi $p |- ( ( H e. CH /\ F : NN --> H /\ F ~~>v A ) -> A e. H ) $=
      ( vf vx cch wcel cn wf chli wbr cv wa wi wal csh cvv wceq syl simprbi fex
      isch2 nnex mpan2 adantr breq1 anbi12d imbi1d albidv cla4gv anbi2d imbi12d
      feq1 breq2 eleq1 cla4v syl6 pm2.43b 3impib ) CGHZICBJZBAKLZACHZVAICEMZJZV
      EFMZKLZNZVGCHZOZFPZEPZVBVCNZVDOZVACQHVMFECUCUAVMVNVDVNBRHZVMVOOVBVPVCVBIR
      HVPUDICRBUBUEUFVPVMVBBVGKLZNZVJOZFPZVOVLVTEBRVEBSZVKVSFWAVIVRVJWAVFVBVHVQ
      ICVEBUNVEBVGKUGUHUIUJUKVSVOFADVGASZVRVNVJVDWBVQVCVBVGABKUOULVGACUPUMUQURT
      USTUT $.
      $( [14-Apr-2008] $) $( [14-Sep-1999] $)
  $}

  ${
    $d x y z $.
    $( The zero sequence in Hilbert space converges to the zero vector.  (The
       proof was shortened by Mario Carneiro, 14-May-2014.) $)
    hlim0 $p |- ( NN X. { 0h } ) ~~>v 0h $=
      ( c0v csn cxp chli wbr cva csm cop cno cims cfv chil wcel df-hba ax-hv0cl
      cn eqid c1 ax-mp wf copn clm cmap co cres wa hhnv h2hlm breqi elexi brres
      bitri ctop cz hhmet opntop 1z cuni wceq imsbai uniopn2 nnuz lmconst mp3an
      cme wss fconst snssi fss mp2an hlex nnex elmap mpbir mpbir2an ) PABZCZADE
      ZVQAFGHIHZJKZUAKZUBKZEZVQLPUCUDZMZVRVQAWBWDUEZEWCWEUFVQADWFVTVSWAVSQZVSWG
      UGZNVTQZWAQZUHUIVQAWBWDALOUJZUKULWAUMMZALMZRUNMWCVTVEMZWLVTVSWGWIUOZVTWAW
      JUPSOUQAWARLPWNLWAURUSWOVTWALVTVSLNWIWHUTWJVASVBVCVDWEPLVQTZPVPVQTVPLVFZW
      PPAWKVGWMWQOALVHSPVPLVQVIVJLPVQVSLNVKVLVMVNVO $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x F $.
    $( If a sequence in Hilbert space subset converges to a limit, it is a
       Cauchy sequence.  (The proof was shortened by Mario Carneiro,
       14-May-2014.) $)
    hlimcaui $p |- ( F ~~>v A -> F e. Cauchy ) $=
      ( chli wbr cdm wcel ccau wrel cva csm cop cno cims cfv df-hba cin eqsstri
      eqid wss ax-mp copn chil cn cmap co cres relres hhnv h2hlm releqi releldm
      clm mpbir mpan resss dmss hhmet lmcau sstri dmeqi dmres eqtri inss1 ssini
      cca cme h2hcau sseqtr4i sseli syl ) BACDZBCEZFZBGFCHZVKVMVNIJKLKZMNZUANZU
      LNZUBUCUDUEZUFZHVRVSUGCVTVPVOVQVORZVOWAUHZOVPRZVQRZUIZUJUMBACUKUNVLGBVLVP
      VENZVSPGVLWFVSVLVREZWFCVRSVLWGSCVTVRWEVRVSUOQCVRUPTVPVFFWGWFSVPVOWAWCUQVP
      VQWDURTUSVLVSWGPZVSVLVTEWHCVTWEUTVRVSVAVBVSWGVCQVDVPVOWAWBOWCVGVHVIVJ $.
      $( [14-May-2014] $) $( [17-Aug-1999] $)
  $}

  $( Function-like behavior of the convergence relation.  (Contributed by Mario
     Carneiro, 14-May-2014.) $)
  hlimf $p |- ~~>v : dom ~~>v --> ~H $=
    ( vx chli cdm chil wf wfn cv cfv wcel wral ffnfv wfun cva csm cop cims copn
    cno eqid ax-mp clm cn cmap co cres cme hhmet methaus lmfun mp2b funres hhnv
    cha df-hba h2hlm funeqi mpbir funfn mpbi wbr wb funfvbrb fvex hlimveci rgen
    sylbi mpbir2an ) BCZDBEBVHFZAGZBHZDIZAVHJAVHDBKBLZVIVMMNOROZPHZQHZUAHZDUBUC
    UDZUEZLZVQLZVTVOUFIVPUMIWAVOVNVNSZVOSZUGVOVPVPSZUHVPUIUJVRVQUKTBVSVOVNVPWBV
    NWBULUNWCWDUOUPUQZBURUSVLAVHVJVHIZVJVKBUTZVLVMWFWGVAWEVJBVBTVKVJVJBVCVDVFVE
    VG $.
    $( [14-May-2014] $)

  ${
    $d x y z w v A $.  $d x y z w v B $.  $d x y z w v F $.
    hlimuni.1 $e |- A e. _V $.
    hlimuni.2 $e |- B e. _V $.
    $( A Hilbert space sequence converges to at most one limit.  (The proof was
       shortened by Mario Carneiro, 14-May-2014.) $)
    hlimunii $p |- ( ( F ~~>v A /\ F ~~>v B ) -> A = B ) $=
      ( chli wbr wa cfv wceq wfun wi cdm chil hlimf ffun ax-mp funbrfv adantr
      wf adantl eqtr3d ) CAFGZCBFGZHCFIZABUCUEAJZUDFKZUCUFLFMZNFTUGOUHNFPQZCAFD
      RQSUDUEBJZUCUGUDUJLUICBFERQUAUB $.
      $( [14-May-2014] $) $( [19-Aug-1999] $)
  $}

  ${
    $d x y F $.  $d x y H $.
    $( The limit of a Hilbert space sequence is unique. $)
    hlimreui $p |- ( E. x e. H F ~~>v x <-> E! x e. H F ~~>v x ) $=
      ( vy cv chli wbr wrex wa wceq wi wral wreu vex hlimunii a1i rgen2 biantru
      wcel breq2 reu4 bitr4i ) BAEZFGZACHZUEUDBDEZFGZIUCUFJKZDCLACLZIUDACMUIUEU
      HADCCUHUCCSUFCSIUCUFBANDNOPQRUDUGADCUCUFBFTUAUB $.
      $( [14-May-2014] $) $( [19-Aug-1999] $)

    $( The limit of a Hilbert space sequence is unique.  (The proof was
       shortened by Mario Carneiro, 14-May-2014.) $)
    hlimeui $p |- ( E. x F ~~>v x <-> E! x F ~~>v x ) $=
      ( cv chli wbr cvv wrex wreu wex weu hlimreui rexv reuv 3bitr3i ) BACDEZAF
      GOAFHOAIOAJABFKOALOAMN $.
      $( [14-May-2014] $) $( [19-Aug-1999] $)
  $}

  ${
    $d x f H $.
    $( A Hilbert subspace is closed iff it is complete.  A complete subspace is
       one in which every Cauchy sequence of vectors in the subspace converges
       to a member of the subspace (Definition of complete subspace in [Beran]
       p. 96).  Remark 3.12 of [Beran] p. 107.  (The proof was shortened by
       Mario Carneiro, 23-Dec-2013.) $)
    isch3 $p |- ( H e. CH <-> ( H e. SH /\ A. f e. Cauchy
                  ( f : NN --> H -> E. x e. H f ~~>v x ) ) ) $=
      ( cch wcel cv wa wi wal wrex ccau wex chil rexex syl ex com12 ax-17 bitri
      hbim csh cn wf chli wbr wral isch2 ax-hcompl 19.29 sylan2 id an12s simprr
      imp jca eximdv df-rex syl6ibr hbre1 hlimcaui imim1i weu sylib ancom exbii
      hlimeui biimpi eupick a2i imim2i bi2.04 impexp alrimi impbii albii df-ral
      syl2anc 3imtr4i bitr4i anbi2i ) CDECUAEZUBCBFZUCZWBAFZUDUEZGZWDCEZHZAIZBI
      ZGWAWCWEACJZHZBKUFZGABCUGWJWMWAWJWBKEZWLHZBIWMWIWOBWIWOWIWNWLWIWNGWHWEGZA
      LZWLWNWIWEALZWQWNWEAMJWRAWBUHWEAMNOWHWEAUIUJWQWCWGWEGZALZWKWCWQWTWCWPWSAW
      CWPWSWCWPGWGWEWHWCWEWGWHWFWGWHUKUNULWCWHWEUMUOPUPQWEACUQZUROPWOWHAWNWLAWN
      ARWCWKAWCARWEACUSTTWCWNWKHZHWCWEWGHZHWOWHXBXCWCXBWEWKHXCWEWNWKWDWBUTVAWEW
      KWGWKWEWGWKWEAVBZWEWGGZALZXCWKWRXDWEACNAWBVFVCWKXFWKWTXFXAWSXEAWGWEVDVESV
      GWEWGAVHVQQVIOVJWNWCWKVKWCWEWGVLVRVMVNVOWLBKVPVSVTS $.
      $( [14-May-2014] $) $( [24-Dec-2001] $)
  $}

  ${
    $d x f H $.  $d x f F $.
    $( Completeness of a closed subspace of Hilbert space. $)
    chcompl $p |- ( ( H e. CH /\ F e. Cauchy /\ F : NN --> H ) ->
                      E. x e. H F ~~>v x ) $=
      ( vf cch wcel ccau cn wf cv chli wbr wrex wi wral isch3 simprbi wceq feq1
      csh breq1 rexbidv imbi12d rcla4cv syl 3imp ) CEFZBGFZHCBIZBAJZKLZACMZUGHC
      DJZIZUMUJKLZACMZNZDGOZUHUIULNZNUGCTFURADCPQUQUSDBGUMBRZUNUIUPULHCUMBSUTUO
      UKACUMBUJKUAUBUCUDUEUF $.
      $( [13-Apr-2008] $) $( [4-Oct-1999] $)
  $}

  ${
    $d x y f $.
    $( The unit Hilbert lattice element (which is all of Hilbert space) belongs
       to the Hilbert lattice.  Part of Proposition 1 of [Kalmbach] p. 65. $)
    helch $p |- ~H e. CH $=
      ( vf vx vy chil cch wcel csh cn cv wf chli wbr wa wi wal co wral mpbir2an
      cc pm3.2i isch2 wss c0v cva csm issh2 ssid ax-hv0cl hvaddcl hvmulcl rgen2
      rgen2a vex hlimveci adantl gen2 ) DEFDGFZHDAIZJZURBIZKLZMUTDFZNZBOAOBADUA
      UQDDUBZUCDFZMUTCIZUDPDFZCDQBDQZUTVFUEPDFZCDQBSQZMBCDUFVDVEDUGUHTVHVJVGBCD
      UTVFUIULVIBCSDUTVFUJUKTRVCABVAVBUSUTURBUMUNUOUPR $.
      $( [6-Sep-1999] $)
  $}

  $( Hilbert space is a subspace of Hilbert space. $)
  helsh $p |- ~H e. SH $=
    ( chil helch chshii ) ABC $.
    $( [2-Jun-2004] $)

  $( Subspaces are subsets of Hilbert space. $)
  shsspwh $p |- SH C_ ~P ~H $=
    ( vx csh cuni cpw chil pwuni wcel cv wss wral wceq helsh shss ssunieq mp2an
    rgen pweqi sseqtr4i ) BBCZDEDBFESEBGAHZEIZABJESKLUAABTMPAEBNOQR $.
    $( [24-Nov-2004] $)

  $( Closed subspaces are subsets of Hilbert space. $)
  chsspwh $p |- CH C_ ~P ~H $=
    ( cch csh chil cpw chsssh shsspwh sstri ) ABCDEFG $.
    $( [24-Nov-2004] $)

  ${
    $d x y f $.
    $( The zero subspace belongs to the set of closed subspaces of Hilbert
       space. $)
    hsn0elch $p |- { 0h } e. CH $=
      ( vf vx vy c0v wcel cn cv chli wbr wa wal chil cva co csm ax-hv0cl pm3.2i
      wral cc wceq csn cch csh wf wi isch2 issh2 snssi ax-mp elexi snid opreq12
      wss elsn hvaddid2i syl6eq oprex elsnc sylibr syl2anb rgen2a opreq2 hvmul0
      sylan9eqr sylan2b rgen2 mpbir2an cxp fconst2 hlim0 breq1 mpbiri sylbi vex
      wb hlimunii eleq1d sylan mpbii gen2 ) DUAZUBEWAUCEZFWAAGZUDZWCBGZHIZJZWEW
      AEZUEZBKAKBAWAUFWBWALUMZDWAEZJWECGZMNZWAEZCWARBWARZWEWLONZWAEZCWARBSRZJBC
      WAUGWJWKDLEWJPDLUHUIDDLPUJZUKZQWOWRWNBCWAWHWEDTZWLDTZWNWLWAEZBDUNCDUNZXAX
      BJZWMDTWNXEWMDDMNDWEDWLDMULDPUOUPWMDWEWLMUQURUSUTVAWQBCSWAXCWESEZXBWQXDXF
      XBJWPDTWQXBXFWPWEDONDWLDWEOVBWEVCVDWPDWEWLOUQURUSVEVFQVGWIABWGWKWHWTWDWCD
      HIZWFWKWHVOWDWCFWAVHZTZXGFDWCWSVIXIXGXHDHIVJWCXHDHVKVLVMXGWFJDWEWADWEWCWS
      BVNVPVQVRVSVTVG $.
      $( [14-Oct-1999] $)
  $}

  $( From any nonzero Hilbert space vector, construct a vector whose norm is
     1. $)
  norm1 $p |- ( ( A e. ~H /\ A =/= 0h ) ->
               ( normh ` ( ( 1 / ( normh ` A ) ) .h A ) ) = 1 ) $=
    ( chil wcel wne wa c1 cno cfv co cmul cc wceq cr cc0 adantr syl2anc cle wbr
    recnd 1re c0v cdiv csm cabs normcl normne0 biimpar rereccl norm-iii normgt0
    simpl clt biimpa 0re ltleii divge0 mpanl12 absid opreq1d mulcom recid eqtrd
    lt01 3eqtrd ) ABCZAUADZEZFAGHZUBIZAUCIGHZVIUDHZVHJIZVIVHJIZFVGVIKCZVEVJVLLV
    GVIVGVHMCZVHNDZVIMCZVEVOVFAUEZOZVEVPVFAUFUGZVHUHPZSZVEVFUKVIAUIPVGVKVIVHJVG
    VQNVIQRZVKVILWAVGVONVHULRZWCVSVEVFWDAUJUMFMCNFQRVOWDEWCTNFUNTVCUOFVHUPUQPVI
    URPUSVGVMVHVIJIZFVGVNVHKCZVMWELWBVEWFVFVEVHVRSOZVIVHUTPVGWFVPWEFLWGVTVHVAPV
    BVD $.
    $( [7-Feb-2006] $)

  ${
    $d x z H $.  $d y z H $.
    norm1ex.1 $e |- H e. SH $.
    $( A normalized vector exists in a subspace iff the subspace has a nonzero
       vector. $)
    norm1exi $p |- ( E. x e. H x =/= 0h <-> E. y e. H ( normh ` y ) = 1 ) $=
      ( vz cv c0v wne wrex cno cfv c1 wceq neeq1 cbvrexv wcel cc0 syl syl2anc
      co wa cdiv csm cc cr sheli normcl adantr wb normne0 biimpar rereccl recnd
      chil simpl csh shmulcl mp3an1 norm1 sylan fveq2 rcla4ev rexlimiva ax-1ne0
      eqeq1d wn df-ne mpbi eqeq1 mtbiri norm-i necon3bbid syl5ib reximia impbii
      sylib bitri ) AFZGHZACIEFZGHZECIZBFZJKZLMZBCIZVSWAAECVRVTGNOWBWFWAWFECVTC
      PZWAUAZLVTJKZUBTZVTUCTZCPZWKJKZLMZWFWHWJUDPZWGWLWHWJWHWIUEPZWIQHZWJUEPWGW
      PWAWGVTUNPZWPVTCDUFZVTUGRUHWGWQWAWGWRWQWAUIWSVTUJRUKWIULSUMWGWAUOCUPPWOWG
      WLDWJVTCUQURSWGWRWAWNWSVTUSUTWEWNBWKCWCWKMWDWMLWCWKJVAVEVBSVCWFWCGHZBCIWB
      WEWTBCWEWDQMZVFWCCPZWTWEXALQMZLQHXCVFVDLQVGVHWDLQVIVJXBXAWCGXBWCUNPXAWCGM
      UIWCCDUFWCVKRVLVMVNWTWABECWCVTGNOVPVOVQ $.
      $( [9-Apr-2006] $)
  $}

  ${
    $d x z $.  $d y z $.
    $( A normalized vector can exist only iff the Hilbert space has a nonzero
       vector. $)
    norm1hex $p |- ( E. x e. ~H x =/= 0h <-> E. y e. ~H ( normh ` y ) = 1 ) $=
      ( chil helsh norm1exi ) ABCDE $.
      $( [21-Jan-2006] $)
  $}

