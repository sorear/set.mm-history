$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Closed subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of closed subspaces of a Hilbert space.  A closed subspace
     is one in which the limit of every convergent sequence in the subspace
     belongs to the subspace.  For its membership relation, see ~ isch .  From
     Definition of [Beran] p. 107.  Alternate definitions are given by ~ isch2
     and ~ isch3 . $)
  df-ch $a |- CH = { h e. SH | ( ~~>v " ( h ^m NN ) ) C_ h } $.

  ${
    $d h H $.
    $( Closed subspace ` H ` of a Hilbert space.  (Contributed by Mario
       Carneiro, 23-Dec-2013.) $)
    isch $p |- ( H e. CH <-> ( H e. SH /\ ( ~~>v " ( H ^m NN ) ) C_ H ) ) $=
      ( vh chli cv cn cmap co cima wss csh wceq opreq1 imaeq2d id sseq12d df-ch
      cch elrab2 ) CBDZEFGZHZSICAEFGZHZAIBAJQSAKZUAUCSAUDTUBCSAEFLMUDNOBPR $.
      $( [23-Dec-2013] $) $( [17-Aug-1999] $)
  $}

  ${
    $d x f H $.
    $( Closed subspace ` H ` of a Hilbert space.  Definition of [Beran]
       p. 107. $)
    isch2 $p |- ( H e. CH <-> ( H e. SH /\ A. f A. x ( ( f : NN --> H /\
        f ~~>v x ) -> x e. H ) ) ) $=
      ( cch wcel csh chli cn cmap co cima wss wa cv wf wbr wi wal isch cvv nnex
      wex 19.23v elima2 imbi1i bitr4i albii alcom dfss2 3bitr4i wb elmapg mpan2
      vex anbi1d imbi1d 2albidv syl5bbr pm5.32i bitri ) CDECFEZGCHIJZKZCLZMVAHC
      BNZOZVEANZGPZMZVGCEZQZARBRZMCSVAVDVLVDVEVBEZVHMZVJQZARBRZVAVLVOBRZARVGVCE
      ZVJQZARVPVDVQVSAVQVNBUBZVJQVSVNVJBUCVRVTVJBVGGVBAUNUDUEUFUGVOBAUHAVCCUIUJ
      VAVOVKBAVAVNVIVJVAVMVFVHVAHTEVMVFUKUACHVEFTULUMUOUPUQURUSUT $.
      $( [23-Dec-2013] $) $( [17-Aug-1999] $)
  $}

  $( A closed subspace is a subspace. $)
  chsh $p |- ( H e. CH -> H e. SH ) $=
    ( cch wcel csh chli cn cmap co cima wss isch simplbi ) ABCADCEAFGHIAJAKL $.
    $( [23-Dec-2013] $) $( [19-Oct-1999] $)

  $( Closed subspaces are subspaces in a Hilbert space. $)
  chsssh $p |- CH C_ SH $=
    ( vx cch csh cv chsh ssriv ) ABCADEF $.
    $( [23-Dec-2013] $) $( [29-May-1999] $)

  $( The set of closed subspaces of a Hilbert space exists (is a set). $)
  chex $p |- CH e. _V $=
    ( cch csh shex chsssh ssexi ) ABCDE $.
    $( [23-Oct-1999] $)

  ${
    chshi.1 $e |- H e. CH $.
    $( A closed subspace is a subspace. $)
    chshii $p |- H e. SH $=
      ( cch wcel csh chsh ax-mp ) ACDAEDBAFG $.
      $( [19-Oct-1999] $)
  $}

  ${
    $d x y H $.
    $( The zero vector belongs to any closed subspace of a Hilbert space. $)
    ch0 $p |- ( H e. CH -> 0h e. H ) $=
      ( cch wcel csh c0v chsh sh0 syl ) ABCADCEACAFAGH $.
      $( [24-Aug-1999] $)

    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chss $p |- ( H e. CH -> H C_ ~H ) $=
      ( cch wcel csh chil wss chsh shss syl ) ABCADCAEFAGAHI $.
      $( [24-Aug-1999] $)

  $}

  $( A member of a closed subspace of a Hilbert space is a vector. $)
  chel $p |- ( ( H e. CH /\ A e. H ) -> A e. ~H ) $=
    ( cch wcel chil chss sseld imp ) BCDZABDAEDIBEABFGH $.
    $( [15-Dec-2004] $)

  ${
    chssi.1 $e |- H e. CH $.
    $( A closed subspace of a Hilbert space is a subset of Hilbert space. $)
    chssii $p |- H C_ ~H $=
      ( chshii shssii ) AABCD $.
      $( [6-Oct-1999] $)

    $( A member of a closed subspace of a Hilbert space is a vector. $)
    cheli $p |- ( A e. H -> A e. ~H ) $=
      ( chil chssii sseli ) BDABCEF $.
      $( [6-Oct-1999] $)

    ${
      cheli.1 $e |- A e. H $.
      $( A member of a closed subspace of a Hilbert space is a vector. $)
      chelii $p |- A e. ~H $=
        ( chil chssii sselii ) BEABCFDG $.
        $( [6-Oct-1999] $)
    $}
  $}

  ${
    $d x f F $.  $d x f H $.  $d x A $.
    chlim.1 $e |- A e. _V $.
    $( The limit property of a closed subspace of a Hilbert space. $)
    chlimi $p |- ( ( H e. CH /\ F : NN --> H /\ F ~~>v A ) -> A e. H ) $=
      ( vf vx cch wcel cn wf chli wbr cv wa wi wal csh cvv wceq syl simprbi fex
      isch2 nnex mpan2 adantr breq1 anbi12d imbi1d albidv cla4gv anbi2d imbi12d
      feq1 breq2 eleq1 cla4v syl6 pm2.43b 3impib ) CGHZICBJZBAKLZACHZVAICEMZJZV
      EFMZKLZNZVGCHZOZFPZEPZVBVCNZVDOZVACQHVMFECUCUAVMVNVDVNBRHZVMVOOVBVPVCVBIR
      HVPUDICRBUBUEUFVPVMVBBVGKLZNZVJOZFPZVOVLVTEBRVEBSZVKVSFWAVIVRVJWAVFVBVHVQ
      ICVEBUNVEBVGKUGUHUIUJUKVSVOFADVGASZVRVNVJVDWBVQVCVBVGABKUOULVGACUPUMUQURT
      USTUT $.
      $( [14-Apr-2008] $) $( [14-Sep-1999] $)
  $}

  ${
    $d x y z $.
    $( The zero sequence in Hilbert space converges to the zero vector. $)
    hlim0 $p |- ( NN X. { 0h } ) ~~>v 0h $=
      ( vx vy vz cn c0v wbr chil wcel cc0 cv clt cle cfv cmv cno wi cr ax-hv0cl
      wral c1 csn cxp chli wf wa co wrex nnex snex elexi hlimi wss fconst snssi
      xpex ax-mp mp2an pm3.2i fvconst2 opreq1d wceq hvsubid syl6eq fveq2d norm0
      fss breq1d biimprd a1d com3r ralrimiv 1nn jctil breq1 ralbidv rcla4ev syl
      imbi1d a1i rgen mpbir2an ) DEUAZUBZEUCFDGWCUDZEGHZUEIAJZKFZBJZCJZLFZWIWCM
      ZENUFZOMZWFKFZPZCDSZBDUGZPZAQSABCEWCDWBUHEUIUOEGRUJZUKWDWEDWBWCUDWBGULZWD
      DEWSUMWEWTREGUNUPDWBGWCVFUQRURWRAQWRWFQHWGTDHZTWILFZWNPZCDSZUEWQWGXDXAWGX
      CCDWIDHZXBWGWNXEWGWNPXBXEWNWGXEWMIWFKXEWMEOMIXEWLEOXEWLEENUFZEXEWKEENDEWI
      WSUSUTWEXFEVAREVBUPVCVDVEVCVGVHVIVJVKVLVMWPXDBTDWHTVAZWOXCCDXGWJXBWNWHTWI
      LVNVRVOVPVQVSVTWA $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x y z w v A $.  $d x y z w v F $.
    hlimcau.1 $e |- A e. _V $.
    hlimcau.2 $e |- F e. _V $.
    ${
      hlimcaui.4 $e |- F ~~>v A $.
      $( If a sequence in Hilbert space subset converges to a limit, it is a
         Cauchy sequence. $)
      hlimcauii $p |- F e. Cauchy $=
        ( vy vz vw wcel cn chil cc0 clt wbr wa cfv co wi wral cr vx vv ccau cle
        wf cv cmv cno wrex hcau chli hlimseqi ax-mp c2 cdiv rehalfcl adantr cif
        wceq breq2 opreq1 breq2d imbi12d 0re elimel 2pos divgt0i2i dedth imbi2d
        2re imp rexralbidv hlimveci hlim2 mpbii mp2an vtoclri sylc raaanv fveq2
        opreq1d fveq2d breq1d cbvralv anbi2i anidm 3bitri prth ffvelrni normsub
        jctil wb anbi2d adantl anim1i ancoms norm3lemt syl sylbird sylan2 syl9r
        an4s ralimdva syl5bir reximdv mpd ex rgen mpbir2an ) BUCIJKBUEZLUAUFZMN
        ZFUFZGUFZUDNZXMHUFZUDNZOZXNBPZXPBPZUGQUHPXKMNZRZHJSZGJSZFJUIZRZUATSUAFG
        HBUJBAUKNZXJEABDCULUMZYFUATXKTIZXLYEYIXLOZXOXSAUGQZUHPZXKUNUOQZMNZRZGJS
        ZFJUIZYEYJYMTIZLYMMNZYQYIYRXLXKUPUQYIXLYSYIXLYSRLYIXKLURZMNZLYTUNUOQZMN
        ZRXKLXKYTUSZXLUUAYSUUCXKYTLMUTUUDYMUUBLMXKYTUNUOVAVBVCYTUNXKLTVDVEVJVFV
        GVHVKLUBUFZMNZXOYLUUEMNZRZGJSFJUIZRZYSYQRUBYMTUUEYMUSZUUFYSUUIYQUUEYMLM
        UTUUKUUHYOFGJJUUKUUGYNXOUUEYMYLMUTVIVLVCXJAKIZUUJUBTSZYHYGUULEABDCVMUMZ
        XJUULOYGUUMEUBFGABVNVOVPVQVRYIYQYERXLYIYPYDFJYPYOXQXTAUGQZUHPZYMMNZRZOZ
        HJSZGJSZYIYDUVAYPUURHJSZOYPYPOYPYOUURGHJVSUVBYPYPUURYOHGJXPXNUSZXQXOUUQ
        YNXPXNXMUDUTUVCUUPYLYMMUVCUUOYKUHUVCXTXSAUGXPXNBVTWAWBWCVCWDWEYPWFWGYIU
        UTYCGJYIXNJIZOZUUSYBHJUUSXRYNUUQOZUVEXPJIZOYAXOYNXQUUQWHUVGUVEUULXTKIZO
        ZUVFYARUVGUVHUULJKXPBYHWIUUNWKUVEUVIOZUVFYNAXTUGQUHPZYMMNZOZYAUVIUVMUVF
        WLUVEUVIUVLUUQYNUVIUVKUUPYMMAXTWJWCWMWNUVJXSKIZUVHOZUULYIOZOZUVMYARUVIU
        VEUVQUULYIUVHUVDUVQUVHUVDOZUVPUVQUVRUVOUVPUVDUVHUVOUVDUVNUVHJKXNBYHWIWO
        WPWOWPXBWPXSXTAXKWQWRWSWTXAXCXCXDXEUQXFXGXHXI $.
        $( [17-Aug-1999] $)
    $}

    $( If a sequence in Hilbert space subset converges to a limit, it is a
       Cauchy sequence. $)
    hlimcaui $p |- ( F ~~>v A -> F e. Cauchy ) $=
      ( chli wbr ccau wcel c0v csn cxp cif eleq1 chil ax-hv0cl elexi ifex breq1
      cn breq2 nnex snex xpex hlim0 elimhyp2v hlimcauii dedth ) BAEFZBGHUHBSIJZ
      KZLZGHBUJBUKGMUHAILZUKUHAICINOPQUHBUJDSUIUAIUBUCQUHUKAEFUKULEFUJIEFUKIEFB
      AUJIBUKAERAULUKETUJUKIERIULUKETUDUEUFUG $.
      $( [17-Aug-1999] $)
  $}

  ${
    $d x y z w v A $.  $d x y z w v B $.  $d x y z w v F $.
    hlimuni.1 $e |- A e. _V $.
    hlimuni.2 $e |- B e. _V $.
    hlimuni.3 $e |- F e. _V $.
    ${
      hlimunii.3 $e |- ( F ~~>v A /\ F ~~>v B ) $.
      $( A Hilbert space sequence converges to at most one limit. $)
      hlimuniii $p |- A = B $=
        ( vy vz vw vx clt wbr wn wa cn wrex wral wi wcel cmv co c0v cc0 cno cfv
        wceq cv cle nn2ge rgen2a c2 cdiv chli chil simpli hlimveci ax-mp simpri
        hvsubcli normcli 2pos divgt0i2i cr 2ne0 redivcli hlimconvi breq2 imbi2d
        2re rexralbidv imbi12d rcla4v mp2 jca r19.26 ltnri wf hlimseqi ffvelrni
        prth normsub mpan2 syl breq1d anbi1d norm3lemt mpanl12 sylbid mtoi con3
        syl5com ralimia sylbir reximi reeanv ralnex rexbii rexnal bitri 3imtr3i
        syl5 3syl mt2 wne wb normgt0 necon1bbii mpbi hvsubeq0i ) ABUAUBZUCUGZAB
        UGUDXKUEUFZLMZNXLXNHUHZIUHZUIMZJUHZXPUIMZOZIPQZJPRZHPRZYAHJPIXOXRUJUKXN
        UDXMULUMUBZLMZXQXPCUFZAUAUBUEUFZYDLMZSZIPRZHPQZXSYFBUAUBUEUFZYDLMZSZIPR
        ZJPQZOZYCNZXMULXKABCAUNMZAUOTZYSCBUNMZGUPZACFDUQURZUUABUOTZYSUUAGUSZBCF
        EUQURZUTZVAZVJVBVCYEYKYPYDVDTZUDKUHZLMZXQYGUUJLMZSZIPRHPQZSZKVDRZYEYKSZ
        XMULUUHVJVEVFZYSUUPUUBKHIACFDVGURUUOUUQKYDVDUUJYDUGZUUKYEUUNYKUUJYDUDLV
        HZUUSUUMYIHIPPUUSUULYHXQUUJYDYGLVHVIVKVLVMVNUUIUUKXSYLUUJLMZSZIPRJPQZSZ
        KVDRZYEYPSZUURUUAUVEUUEKJIBCFEVGURUVDUVFKYDVDUUSUUKYEUVCYPUUTUUSUVBYNJI
        PPUUSUVAYMXSUUJYDYLLVHVIVKVLVMVNVOYJYOOZJPQZHPQXTNZIPRZJPQZHPQZYQYRUVHU
        VKHPUVGUVJJPUVGYIYNOZIPRUVJYIYNIPVPUVMUVIIPUVMXTYHYMOZSZXPPTZUVIXQYHXSY
        MWAUVPUVNNUVOUVIUVPUVNXMXMLMZXMUUHVQUVPUVNAYFUAUBUEUFZYDLMZYMOZUVQUVPYH
        UVSYMUVPYGUVRYDLUVPYFUOTZYGUVRUGZPUOXPCYSPUOCVRUUBACFDVSURVTZUWAYTUWBUU
        CYFAWBWCWDWEWFUVPUWAUVTUVQSZUWCUWAXMVDTZUWDUUHYTUUDUWAUWEOUWDUUCUUFABYF
        XMWGWHWCWDWIWJXTUVNWKWLXBWMWNWOWOYJYOHJPPWPUVLYBNZHPQYRUVKUWFHPUVKYANZJ
        PQUWFUVJUWGJPXTIPWQWRYAJPWSWTWRYBHPWSWTXAXCXDXNXKUCXKUOTXKUCXEXNXFUUGXK
        XGURXHXIABUUCUUFXJXI $.
        $( [19-Aug-1999] $)
    $}

    $( A Hilbert space sequence converges to at most one limit. $)
    hlimunii $p |- ( ( F ~~>v A /\ F ~~>v B ) -> A = B ) $=
      ( chli wbr wa wceq c0v cif ifex breq2 anbi1d anbi2d breq1 anbi12d hlim0
      cn eqeq1 eqeq2 csn chil ax-hv0cl elexi nnex snex xpex elimhyp3v hlimuniii
      cxp pm3.2i dedth2v ) CAGHZCBGHZIZABJUQAKLZBJURUQBKLZJABKKAURBUABUSURUBURU
      SUQCTKUCZULZLZUQAKDKUDUEUFZMUQBKEVCMUQCVAFTUTUGKUHUIMUQCURGHZUPIVDCUSGHZI
      VBURGHZVBUSGHZIVAKGHZVHIVAURGHZVHIVIVAUSGHZIABCKKVAAURJUOVDUPAURCGNOBUSJU
      PVEVDBUSCGNPCVBJVDVFVEVGCVBURGQCVBUSGQRKURJVHVIVHKURVAGNOKUSJVHVJVIKUSVAG
      NPVAVBJVIVFVJVGVAVBURGQVAVBUSGQRVHVHSSUMUJUKUN $.
      $( [19-Aug-1999] $)
  $}

  ${
    $d x y F $.  $d x y H $.
    hlimreu.1 $e |- F e. _V $.
    $( The limit of a Hilbert space sequence is unique. $)
    hlimreui $p |- ( E. x e. H F ~~>v x <-> E! x e. H F ~~>v x ) $=
      ( vy cv chli wbr wrex wreu wa wceq wral wcel vex hlimunii a1i rgen2a jctr
      wi breq2 reu4 sylibr reurex impbii ) BAFZGHZACIZUGACJZUHUHUGBEFZGHZKUFUJL
      TZECMACMZKUIUHUMULAECULUFCNUJCNKUFUJBAOEODPQRSUGUKAECUFUJBGUAUBUCUGACUDUE
      $.
      $( [19-Aug-1999] $)
  $}

  ${
    $d x y F $.  $d x y H $.
    hlimeu.1 $e |- F e. _V $.
    $( The limit of a Hilbert space sequence is unique. $)
    hlimeui $p |- ( E. x F ~~>v x <-> E! x F ~~>v x ) $=
      ( vy cv chli wbr wex weu wa wceq wi wal vex hlimunii gen2 jctr eu4 sylibr
      breq2 euex impbii ) BAEZFGZAHZUDAIZUEUEUDBDEZFGZJUCUGKLZDMAMZJUFUEUJUIADU
      CUGBANDNCOPQUDUHADUCUGBFTRSUDAUAUB $.
      $( [19-Aug-1999] $)
  $}

  ${
    $d x f H $.
    $( A Hilbert subspace is closed iff it is complete.  A complete subspace is
       one in which every Cauchy sequence of vectors in the subspace converges
       to a member of the subspace (Definition of complete subspace in [Beran]
       p. 96).  Remark 3.12 of [Beran] p. 107.  (The proof was shortened by
       Mario Carneiro, 23-Dec-2013.) $)
    isch3 $p |- ( H e. CH <-> ( H e. SH /\ A. f e. Cauchy
                  ( f : NN --> H -> E. x e. H f ~~>v x ) ) ) $=
      ( wcel cv wa wi wal wrex ccau wex chil rexex syl ex com12 ax-17 vex bitri
      hbim cch csh cn wf chli wral isch2 ax-hcompl 19.29 sylan2 id an12s simprr
      wbr imp jca eximdv df-rex syl6ibr hbre1 hlimcaui imim1i weu hlimeui sylib
      ancom exbii biimpi eupick syl2anc a2i imim2i bi2.04 impexp 3imtr4i alrimi
      impbii albii df-ral bitr4i anbi2i ) CUADCUBDZUCCBEZUDZWCAEZUEUNZFZWECDZGZ
      AHZBHZFWBWDWFACIZGZBJUFZFABCUGWKWNWBWKWCJDZWMGZBHWNWJWPBWJWPWJWOWMWJWOFWI
      WFFZAKZWMWOWJWFAKZWRWOWFALIWSAWCUHWFALMNWIWFAUIUJWRWDWHWFFZAKZWLWDWRXAWDW
      QWTAWDWQWTWDWQFWHWFWIWDWFWHWIWGWHWIUKUOULWDWIWFUMUPOUQPWFACURZUSNOWPWIAWO
      WMAWOAQWDWLAWDAQWFACUTTTWDWOWLGZGWDWFWHGZGWPWIXCXDWDXCWFWLGXDWFWOWLWEWCAR
      BRZVAVBWFWLWHWLWFWHWLWFAVCZWFWHFZAKZXDWLWSXFWFACMAWCXEVDVEWLXHWLXAXHXBWTX
      GAWHWFVFVGSVHWFWHAVIVJPVKNVLWOWDWLVMWDWFWHVNVOVPVQVRWMBJVSVTWAS $.
      $( [23-Dec-2013] $) $( [24-Dec-2001] $)
  $}

  ${
    $d x f H $.  $d x f F $.
    $( Completeness of a closed subspace of Hilbert space. $)
    chcompl $p |- ( ( H e. CH /\ F e. Cauchy /\ F : NN --> H ) ->
                      E. x e. H F ~~>v x ) $=
      ( vf cch wcel ccau cn wf cv chli wbr wrex wi wral isch3 simprbi wceq feq1
      csh breq1 rexbidv imbi12d rcla4cv syl 3imp ) CEFZBGFZHCBIZBAJZKLZACMZUGHC
      DJZIZUMUJKLZACMZNZDGOZUHUIULNZNUGCTFURADCPQUQUSDBGUMBRZUNUIUPULHCUMBSUTUO
      UKACUMBUJKUAUBUCUDUEUF $.
      $( [13-Apr-2008] $) $( [4-Oct-1999] $)
  $}

  ${
    $d x y f $.
    $( The unit Hilbert lattice element (which is all of Hilbert space) belongs
       to the Hilbert lattice.  Part of Proposition 1 of [Kalmbach] p. 65. $)
    helch $p |- ~H e. CH $=
      ( vf vx vy chil cch wcel csh cn cv wf chli wbr wa wal co wral cc mpbir2an
      pm3.2i vex wi isch2 wss c0v cva csm issh2 ax-hv0cl hvaddcl rgen2a hvmulcl
      ssid rgen2 hlimveci adantl gen2 ) DEFDGFZHDAIZJZURBIZKLZMUTDFZUAZBNANBADU
      BUQDDUCZUDDFZMUTCIZUEODFZCDPBDPZUTVFUFODFZCDPBQPZMBCDUGVDVEDULUHSVHVJVGBC
      DUTVFUIUJVIBCQDUTVFUKUMSRVCABVAVBUSUTURATBTUNUOUPR $.
      $( [6-Sep-1999] $)
  $}

  $( Hilbert space is a subspace of Hilbert space. $)
  helsh $p |- ~H e. SH $=
    ( chil helch chshii ) ABC $.
    $( [2-Jun-2004] $)

  $( Subspaces are subsets of Hilbert space. $)
  shsspwh $p |- SH C_ ~P ~H $=
    ( vx csh cuni cpw chil pwuni wcel cv wss wral wceq helsh shss ssunieq mp2an
    rgen pweqi sseqtr4i ) BBCZDEDBFESEBGAHZEIZABJESKLUAABTMPAEBNOQR $.
    $( [24-Nov-2004] $)

  $( Closed subspaces are subsets of Hilbert space. $)
  chsspwh $p |- CH C_ ~P ~H $=
    ( cch csh chil cpw chsssh shsspwh sstri ) ABCDEFG $.
    $( [24-Nov-2004] $)

  ${
    $d x y f $.
    $( The zero subspace belongs to the set of closed subspaces of Hilbert
       space. $)
    hsn0elch $p |- { 0h } e. CH $=
      ( vf vx vy c0v wcel cn cv chli wbr wa wal chil cva co csm ax-hv0cl pm3.2i
      wral cc wceq csn cch csh wf wi isch2 issh2 snssi ax-mp elexi snid opreq12
      wss elsn hvaddid2i syl6eq oprex elsnc sylibr syl2anb rgen2a opreq2 hvmul0
      sylan9eqr sylan2b rgen2 mpbir2an cxp fconst2 hlim0 breq1 mpbiri sylbi vex
      wb hlimunii eleq1d sylan mpbii gen2 ) DUAZUBEWAUCEZFWAAGZUDZWCBGZHIZJZWEW
      AEZUEZBKAKBAWAUFWBWALUMZDWAEZJWECGZMNZWAEZCWARBWARZWEWLONZWAEZCWARBSRZJBC
      WAUGWJWKDLEWJPDLUHUIDDLPUJZUKZQWOWRWNBCWAWHWEDTZWLDTZWNWLWAEZBDUNCDUNZXAX
      BJZWMDTWNXEWMDDMNDWEDWLDMULDPUOUPWMDWEWLMUQURUSUTVAWQBCSWAXCWESEZXBWQXDXF
      XBJWPDTWQXBXFWPWEDONDWLDWEOVBWEVCVDWPDWEWLOUQURUSVEVFQVGWIABWGWKWHWTWDWCD
      HIZWFWKWHVOWDWCFWAVHZTZXGFDWCWSVIXIXGXHDHIVJWCXHDHVKVLVMXGWFJDWEWADWEWCWS
      BVNAVNVPVQVRVSVTVG $.
      $( [14-Oct-1999] $)
  $}

  $( From any nonzero Hilbert space vector, construct a vector whose norm is
     1. $)
  norm1 $p |- ( ( A e. ~H /\ A =/= 0h ) ->
               ( normh ` ( ( 1 / ( normh ` A ) ) .h A ) ) = 1 ) $=
    ( chil wcel wne wa c1 cno cfv co cmul cc wceq cr cc0 adantr syl2anc cle wbr
    recnd 1re c0v cdiv csm cabs normcl normne0 biimpar rereccl norm-iii normgt0
    simpl clt biimpa 0re ltleii divge0 mpanl12 absid opreq1d mulcom recid eqtrd
    lt01 3eqtrd ) ABCZAUADZEZFAGHZUBIZAUCIGHZVIUDHZVHJIZVIVHJIZFVGVIKCZVEVJVLLV
    GVIVGVHMCZVHNDZVIMCZVEVOVFAUEZOZVEVPVFAUFUGZVHUHPZSZVEVFUKVIAUIPVGVKVIVHJVG
    VQNVIQRZVKVILWAVGVONVHULRZWCVSVEVFWDAUJUMFMCNFQRVOWDEWCTNFUNTVCUOFVHUPUQPVI
    URPUSVGVMVHVIJIZFVGVNVHKCZVMWELWBVEWFVFVEVHVRSOZVIVHUTPVGWFVPWEFLWGVTVHVAPV
    BVD $.
    $( [7-Feb-2006] $)

  ${
    $d x z H $.  $d y z H $.
    norm1ex.1 $e |- H e. SH $.
    $( A normalized vector exists in a subspace iff the subspace has a nonzero
       vector. $)
    norm1exi $p |- ( E. x e. H x =/= 0h <-> E. y e. H ( normh ` y ) = 1 ) $=
      ( vz cv c0v wne wrex cno cfv c1 wceq neeq1 cbvrexv wcel cc0 syl syl2anc
      co wa cdiv csm cc cr sheli normcl adantr wb normne0 biimpar rereccl recnd
      chil simpl csh shmulcl mp3an1 norm1 sylan fveq2 rcla4ev rexlimiva ax-1ne0
      eqeq1d wn df-ne mpbi eqeq1 mtbiri norm-i necon3bbid syl5ib reximia impbii
      sylib bitri ) AFZGHZACIEFZGHZECIZBFZJKZLMZBCIZVSWAAECVRVTGNOWBWFWAWFECVTC
      PZWAUAZLVTJKZUBTZVTUCTZCPZWKJKZLMZWFWHWJUDPZWGWLWHWJWHWIUEPZWIQHZWJUEPWGW
      PWAWGVTUNPZWPVTCDUFZVTUGRUHWGWQWAWGWRWQWAUIWSVTUJRUKWIULSUMWGWAUOCUPPWOWG
      WLDWJVTCUQURSWGWRWAWNWSVTUSUTWEWNBWKCWCWKMWDWMLWCWKJVAVEVBSVCWFWCGHZBCIWB
      WEWTBCWEWDQMZVFWCCPZWTWEXALQMZLQHXCVFVDLQVGVHWDLQVIVJXBXAWCGXBWCUNPXAWCGM
      UIWCCDUFWCVKRVLVMVNWTWABECWCVTGNOVPVOVQ $.
      $( [9-Apr-2006] $)
  $}

  ${
    $d x z $.  $d y z $.
    $( A normalized vector can exist only iff the Hilbert space has a nonzero
       vector. $)
    norm1hex $p |- ( E. x e. ~H x =/= 0h <-> E. y e. ~H ( normh ` y ) = 1 ) $=
      ( chil helsh norm1exi ) ABCDE $.
      $( [21-Jan-2006] $)
  $}

