$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                  Vector space postulates for a Hilbert space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( This is our first axiom for Hilbert space, which is the foundation
     for quantum mechanics and quantum field theory.  We assume that there
     exists a primitive class, ` H~ ` , which contains objects called vectors
     whose properties are postulated in this axiom (which asserts that it
     is a set) and the axioms ~ ax-hfvadd , ~ ax-hvcom , ~ ax-hvass ,
     ~ ax-hv0cl , ~ ax-hvaddid , ~ ax-hfvmul , ~ ax-hvmulid ,
     ~ ax-hvmulass , ~ ax-hvdistr1 , ~ ax-hvdistr2 , ~ ax-hvmul0 ,
     ~ ax-hfi , ~ ax-his1 , ~ ax-his2 , ~ ax-his3 , ~ ax-his4 , and
     ~ ax-hcompl .

     Earlier, we also developed Hilbert space directly under ZFC starting
     with ~ df-hl .  However, when we are dealing with a fixed Hilbert space,
     the use of separate axioms makes the theorems somewhat less clumsy to
     state and prove.  Compare, for example, ~ hlcom and ~ ax-hvcom .

     We could also specify the following alternate axiomatization for the
     Hilbert Space Explorer, consisting of a single axiom containing a new
     class constant ` H. ` :

         ax-hil $a ` |- H. e. CHil ` $.

     where we define the Hilbert space primitives in terms of ` H. ` :

         df-hilbase $a ` |- H~ = ( Base `` H. ) ` $.

         df-hiladd $a ` |- +h = ( +v `` H. ) ` $.

         df-hilmul $a ` |- .h = ( .s `` H. ) ` $.

         df-hil0 $a ` |- 0h = ( 0v `` H. ) ` $.

         df-hilip $a ` |- .ih = ( .i `` H. ) ` $.

     The existing axioms ~ ax-hilex through ~ ax-hcompl will then follow as
     theorems from ~ hlex , ~ hladdf , ~ hlcom , ~ hlass , ~ hl0cl ,
     ~ hladdid , ~ hlmulf , ~ hlmulid , ~ hlmulass , ~ hldi , ~ hldir ,
     ~ hlmul0 , ~ hlipf , ~ hlipcj , ~ hlipdir , ~ hlipass , ~ hlipgt0 , and
     ~ hlcompl respectively.  (Note that theorem ~ hilcompl will transform
     ~ hlcompl into ~ ax-hcompl .)

     To eliminate the sole axiom ax-hil, we can add a definition equating
     ` H. ` to any class previously proved (under ZFC) to be a Hilbert space.
     Then ax-hil will follow as a theorem, making all theorems in the Hilbert
     Space Explorer become pure ZFC theorems.  For example, we know that
     complex numbers form a Hilbert space by ~ cnhl .  If we add the definition

          df-hil $a ` |- H. = <. <. + , x. >. , abs >. ` $.

     then ax-hil will follow as a theorem by ~ cnhl , so that every theorem
     in the Hilbert Space Explorer will become a theorem of ZFC only, with
     no additional axioms. $)
  ax-hilex $a |- H~ e. V $.

  $( Vector addition is an operation on ` H~ ` . $)
  ax-hfvadd $a |- +h : ( H~ X. H~ ) --> H~ $.

  $( Vector addition is commutative. $)
  ax-hvcom $a |- ( ( A e. H~ /\ B e. H~ ) -> ( A +h B ) = ( B +h A ) ) $.

  $( Vector addition is associative. $)
  ax-hvass $a |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                 ( ( A +h B ) +h C ) = ( A +h ( B +h C ) ) ) $.

  $( The zero vector is in the vector space. $)
  ax-hv0cl $a |- 0h e. H~ $.

  $( Addition with the zero vector. $)
  ax-hvaddid $a |- ( A e. H~ -> ( A +h 0h ) = A ) $.

  $( Scalar multiplication is an operation on ` CC ` and ` H~ ` . $)
  ax-hfvmul $a |- .h : ( CC X. H~ ) --> H~ $.

  $( Scalar multiplication by one. $)
  ax-hvmulid $a |- ( A e. H~ -> ( 1 .h A ) = A ) $.

  $( Scalar multiplication associative law $)
  ax-hvmulass $a |- ( ( A e. CC /\ B e. CC /\ C e. H~ ) ->
                 ( ( A x. B ) .h C ) = ( A .h ( B .h C ) ) ) $.

  $( Scalar multiplication distributive law $)
  ax-hvdistr1 $a |- ( ( A e. CC /\ B e. H~ /\ C e. H~ ) ->
                ( A .h ( B +h C ) ) = ( ( A .h B ) +h ( A .h C ) ) ) $.

  $( Scalar multiplication distributive law $)
  ax-hvdistr2 $a |- ( ( A e. CC /\ B e. CC /\ C e. H~ ) ->
                ( ( A + B ) .h C ) = ( ( A .h C ) +h ( B .h C ) ) ) $.

  $( Scalar multiplication by zero.  We can derive the existence of the
     negative of a vector from this axiom (see ~ hvsubidt and ~ hvsubvalt ). $)
  ax-hvmul0 $a |- ( A e. H~ -> ( 0 .h A ) = 0h ) $.

