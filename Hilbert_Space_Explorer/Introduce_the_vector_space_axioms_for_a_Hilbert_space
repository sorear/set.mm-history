$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Introduce the vector space axioms for a Hilbert space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( This is our first axiom for a complex Hilbert space, which is the
     foundation for quantum mechanics and quantum field theory.  We assume that
     there exists a primitive class, ` ~H ` , which contains objects called
     vectors.

     The 18 axioms for a complex Hilbert space consist of ~ ax-hilex ,
     ~ ax-hfvadd , ~ ax-hvcom , ~ ax-hvass , ~ ax-hv0cl , ~ ax-hvaddid ,
     ~ ax-hfvmul , ~ ax-hvmulid , ~ ax-hvmulass , ~ ax-hvdistr1 ,
     ~ ax-hvdistr2 , ~ ax-hvmul0 , ~ ax-hfi , ~ ax-his1 , ~ ax-his2 ,
     ~ ax-his3 , ~ ax-his4 , and ~ ax-hcompl .

     The axioms specify the properties of 5 primitive symbols, ` ~H ` ,
     ` +h ` , ` .h ` , ` 0h ` , and ` .ih ` .

     If we can prove in ZFC set theory that a class
     ` U = <. <. +h , .h >. , normh >. ` is a complex Hilbert space, i.e. that
     ` U e. CHil ` , then these axioms can be proved as theorems ~ axhilex ,
     ~ axhfvadd , ~ axhvcom , ~ axhvass , ~ axhv0cl , ~ axhvaddid ,
     ~ axhfvmul , ~ axhvmulid , ~ axhvmulass , ~ axhvdistr1 , ~ axhvdistr2 ,
     ~ axhvmul0 , ~ axhfi , ~ axhis1 , ~ axhis2 , ~ axhis3 , ~ axhis4 , and
     ~ axhcompl respectively.  In that case, the theorems of the Hilbert Space
     Explorer will become theorems of ZFC set theory.  See also the comments in
     ~ axhilex . $)
  ax-hilex $a |- ~H e. _V $.

  $( Vector addition is an operation on ` ~H ` . $)
  ax-hfvadd $a |- +h : ( ~H X. ~H ) --> ~H $.

  $( Vector addition is commutative. $)
  ax-hvcom $a |- ( ( A e. ~H /\ B e. ~H ) -> ( A +h B ) = ( B +h A ) ) $.

  $( Vector addition is associative. $)
  ax-hvass $a |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                 ( ( A +h B ) +h C ) = ( A +h ( B +h C ) ) ) $.

  $( The zero vector is in the vector space. $)
  ax-hv0cl $a |- 0h e. ~H $.

  $( Addition with the zero vector. $)
  ax-hvaddid $a |- ( A e. ~H -> ( A +h 0h ) = A ) $.

  $( Scalar multiplication is an operation on ` CC ` and ` ~H ` . $)
  ax-hfvmul $a |- .h : ( CC X. ~H ) --> ~H $.

  $( Scalar multiplication by one. $)
  ax-hvmulid $a |- ( A e. ~H -> ( 1 .h A ) = A ) $.

  $( Scalar multiplication associative law. $)
  ax-hvmulass $a |- ( ( A e. CC /\ B e. CC /\ C e. ~H ) ->
                 ( ( A x. B ) .h C ) = ( A .h ( B .h C ) ) ) $.

  $( Scalar multiplication distributive law. $)
  ax-hvdistr1 $a |- ( ( A e. CC /\ B e. ~H /\ C e. ~H ) ->
                ( A .h ( B +h C ) ) = ( ( A .h B ) +h ( A .h C ) ) ) $.

  $( Scalar multiplication distributive law. $)
  ax-hvdistr2 $a |- ( ( A e. CC /\ B e. CC /\ C e. ~H ) ->
                ( ( A + B ) .h C ) = ( ( A .h C ) +h ( B .h C ) ) ) $.

  $( Scalar multiplication by zero.  We can derive the existence of the
     negative of a vector from this axiom (see ~ hvsubid and ~ hvsubval ). $)
  ax-hvmul0 $a |- ( A e. ~H -> ( 0 .h A ) = 0h ) $.

