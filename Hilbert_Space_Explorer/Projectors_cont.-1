$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Projectors (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    pjorth.1 $e |- A e. H~ $.
    pjorth.2 $e |- B e. H~ $.
    $( Projection components on orthocomplemented subspaces are orthogonal. $)
    pjorth $p |- ( H e. CH ->
          ( ( ( proj ` H ) ` A ) .ih ( ( proj ` ( _|_ ` H ) ) ` B ) ) = 0 ) $=
      ( csh wcel cpj cfv cort wa csp co cc0 wceq cch shocorth chsh chil
      axpjclt mpan2 chocclt syl jca sylc ) CFGACHIIZCGZBCJIZHIIZUHGZKUFUILMNOCP
      GZUFUICQCRUKUGUJUKASGUGDACTUAUKUHPGZUJCUBULBSGUJEBUHTUAUCUDUE $.
      $( [26-Oct-1999] $)
  $}

  $( Property of identity projection.  Remark in [Beran] p. 111. $)
  pjch1t $p |- ( A e. H~ -> ( ( proj ` H~ ) ` A ) = A ) $=
    ( chil wcel cpj cfv wceq wb c0v cif eleq1 fveq2 id eqeq12d bibi12d helch
    ax-hv0cl elimel pjch dedth ibi ) ABCZABDEZEZAFZUAUAUDGUAAHIZBCZUEUBEZUEFZGA
    HAUEFZUAUFUDUHAUEBJUIUCUGAUEAUEUBKUILMNUEBOAHBPQRST $.
    $( [28-Oct-1999] $)

  $( The orthogonal projection.  Lemma 4.4(i) of [Beran] p. 111. $)
  pjot $p |- ( ( H e. CH /\ A e. H~ ) -> ( ( proj ` ( _|_ ` H ) ) ` A ) =
             ( ( ( proj ` H~ ) ` A ) -h ( ( proj ` H ) ` A ) ) ) $=
    ( cch wcel chil wa cpj cfv cmv co cort wceq cva pjch1t adantl axpjpjt
    eqtr2d wb hvsubaddt helch pjcl pjhclt chocclt sylan syl3anc mpbird eqcomd
    ) BCDZAEDZFZAEGHHZABGHHZIJZABKHZGHHZUJUMUOLZULUOMJZUKLZUJUKAUQUIUKALUHANOAB
    PQUKEDZULEDUOEDZUPURRUJUKULUOSUIUSUHAETUAOABUBUNCDUIUTUHAUNUBBUCUDUEUFUG $.
    $( [30-Oct-1999] $)

  $( Property of the null projection.  Remark in [Beran] p. 111. $)
  pjch0t $p |- ( A e. H~ -> ( ( proj ` 0H ) ` A ) = 0h ) $=
    ( chil wcel c0h cpj cfv cmv co c0v cort cch wceq helch pjot mpan choc1
    fveq2i fveq1i syl5eqr pjhcl hvsubidt syl eqtrd ) ABCZADEFZFZABEFFZUGGHZIUDA
    BJFZEFZFZUHUFBKCUDUKUHLMABNOAUJUEUIDEPQRSUDUGBCUHILABMTUGUAUBUC $.
    $( [7-Nov-2000] $)

  ${
    pjidm.1 $e |- H e. CH $.
    pjidm.2 $e |- A e. H~ $.
    $( A projection is idempotent.  Property (ii) of [Beran] p. 109. $)
    pjidm $p |- ( ( proj ` H ) ` ( ( proj ` H ) ` A ) ) =
               ( ( proj ` H ) ` A ) $=
      ( cpj cfv wcel wceq pjcli pjhcli pjch mpbi ) ABEFZFZBGNMFNHABCDINBCABCDJK
      L $.
      $( [28-Oct-1999] $)

    ${
      pjadj.3 $e |- B e. H~ $.
      $( A projection is self-adjoint.  Property (i) of [Beran] p. 109. $)
      pjadj $p |- ( ( ( proj ` H ) ` A ) .ih B ) =
                  ( A .ih ( ( proj ` H ) ` B ) ) $=
        ( cpj cfv cort cva co csp caddc ccj cc0 cch wcel wceq pjorth ax-mp
        fveq2i cj0 eqtr choccl pjhcli his1 3eqtr4r opreq2i chil his7t mp3an
        ax-his2 3eqtr4 pjpj opreq1i ) ACGHZHZBUPHZBCIHZGHZHZJKZLKZUQAUTHZJKZURL
        KZUQBLKAURLKUQURLKZUQVALKZMKZVGVDURLKZMKZVCVFVHVJVGMURVDLKZNHZOVJVHVMON
        HOVLONCPQZVLORDBACFESTUAUBUCVDURAUSCDUDZEUEZBCDFUEZUFVNVHORDABCEFSTUGUH
        UQUIQZURUIQZVAUIQVCVIRACDEUEZVQBUSVOFUEUQURVAUJUKVRVDUIQVSVFVKRVTVPVQUQ
        VDURULUKUMBVBUQLBCDFUNUHAVEURLACDEUNUOUM $.
        $( [30-Oct-1999] $)

      $( Component of a projection. $)
      pjcomp $p |- ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
                   ( ( proj ` H ) ` ( A +h B ) ) = A ) $=
        ( wcel cort cfv wa cva co cpj wceq hvaddcl pjcli choccl pjpj eqid
        chocuni mp2ani mpanl12 pm3.26d ) ACGBCHIZGJZABKLZCMIIZANZUFUDMIIZBNZUGC
        GZUIUDGZUEUHUJJZUFCDABEFOZPUFUDCDQUNPUKULJUEJUFUGUIKLNUFUFNUMUFCDUNRUFS
        UGUIABUFCDTUAUBUC $.
        $( [31-Oct-1999] $)

      $( Projection of vector sum is sum of projections. $)
      pjadd $p |- ( ( proj ` H ) ` ( A +h B ) ) =
                  ( ( ( proj ` H ) ` A ) +h ( ( proj ` H ) ` B ) ) $=
        ( cva co cpj cfv cort pjpj opreq12i pjhcli choccl hvadd4 eqtr fveq2i
        wcel wceq pjcli csh wa wi chshi shaddclt ax-mp mp2an hvaddcl pjcomp )
        ABGHZCIJZJAULJZBULJZGHZACKJZIJZJZBUQJZGHZGHZULJZUOUKVAULUKUMURGHZUNUSGH
        ZGHVAAVCBVDGACDELBCDFLMUMURUNUSACDENZAUPCDOZENZBCDFNZBUPVFFNZPQRUOCSZUT
        UPSZVBUOTUMCSZUNCSZVJACDEUABCDFUACUBSVLVMUCVJUDCDUEUMUNCUFUGUHURUPSZUSU
        PSZVKAUPVFEUABUPVFFUAUPUBSVNVOUCVKUDUPVFUEURUSUPUFUGUHUOUTCDUMUNVEVHUIU
        RUSVGVIUIUJUHQ $.
        $( [31-Oct-1999] $)
    $}

    $( The inner product of a projection and its argument is the square of the
       norm of the projection.  Remark in [Halmos] p. 44. $)
    pjinorm $p |- ( ( ( proj ` H ) ` A ) .ih A ) =
                 ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) $=
      ( cpj cfv cno c2 cexp co csp pjhcli normsq pjadj pjidm opreq1i 3eqtr2r )
      ABEFZFZGFHIJSSKJSRFZAKJSAKJSABCDLZMSABCUADNTSAKABCDOPQ $.
      $( [13-Aug-2000] $)

    ${
      pjmul.3 $e |- C e. CC $.
      $( Projection of (scalar) product is product of projection. $)
      pjmul $p |- ( ( proj ` H ) ` ( C .h A ) ) =
                  ( C .h ( ( proj ` H ) ` A ) ) $=
        ( csm co cpj cfv cort cva pjpj opreq2i pjhcli choccl hvdistr1 eqtr
        fveq2i wcel wceq cc pjcli csh wa wi chshi shmulcltOLD ax-mp mp2an
        hvmulcl pjcomp ) BAGHZCIJZJBAUNJZGHZBACKJZIJJZGHZLHZUNJZUPUMUTUNUMBUOUR
        LHZGHUTAVBBGACDEMNBUOURFACDEOZAUQCDPZEOZQRSUPCTZUSUQTZVAUPUABUBTZUOCTZV
        FFACDEUCCUDTVHVIUEVFUFCDUGBUOCUHUIUJVHURUQTZVGFAUQVDEUCUQUDTVHVJUEVGUFU
        QVDUGBURUQUHUIUJUPUSCDBUOFVCUKBURFVEUKULUJR $.
        $( [31-Oct-1999] $)
    $}

    ${
      pjsub.3 $e |- B e. H~ $.
      $( Projection of vector difference is difference of projections. $)
      pjsub $p |- ( ( proj ` H ) ` ( A -h B ) ) =
                  ( ( ( proj ` H ) ` A ) -h ( ( proj ` H ) ` B ) ) $=
        ( c1 cneg csm co cva cpj cfv cmv ax1cn negcl hvmulcl pjadd pjmul
        opreq2i eqtr hvsubval fveq2i pjhcli 3eqtr4 ) AGHZBIJZKJZCLMZMZAUIMZUFBU
        IMZIJZKJZABNJZUIMUKULNJUJUKUGUIMZKJUNAUGCDEUFBGOPZFQRUPUMUKKBUFCDFUQSTU
        AUOUHUIABEFUBUCUKULACDEUDBCDFUDUBUE $.
        $( [31-Oct-1999] $)
    $}

    ${
      pjsslem.1 $e |- G e. CH $.
      $( Lemma for subset relationships of projections. $)
      pjsslem $p |- ( ( ( proj ` ( _|_ ` H ) ) ` A ) -h
                      ( ( proj ` ( _|_ ` G ) ) ` A ) ) =
                    ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) $=
        ( cort cfv cpj cmv co c0v c1 cneg csm chil cch wcel wceq pjot mp2an
        opreq12i helch pjcli pjhcli hvsubsub4 hvsubidt ax-mp opreq1i 3eqtr
        hvsubcl hv2neg hvnegdi ) ACGHIHHZABGHIHHZJKZLACIHHZABIHHZJKZJKZMNUSOKUR
        UQJKUPAPIHHZUQJKZVAURJKZJKVAVAJKZUSJKUTUNVBUOVCJCQRAPRZUNVBSDEACTUABQRV
        EUOVCSFEABTUAUBVAUQVAURAPUCEUDZACDEUEZVFABFEUEZUFVDLUSJVAPRVDLSVFVAUGUH
        UIUJUSUQURVGVHUKULUQURVGVHUMUJ $.
        $( [31-Oct-1999] $)

      $( Subset relationship for projections.  Theorem 4.5(i)->(ii) of [Beran]
         p. 112. $)
      pjss2 $p |- ( H (_ G -> ( ( proj ` H ) ` ( ( proj ` G ) ` A ) ) =
                 ( ( proj ` H ) ` A ) ) $=
        ( wss cort cfv cpj cmv co wcel wceq wa chsscon3 choccl pjcli ssel mpi
        sylbi jctil csh wi chshi shsubclt ax-mp syl c0v pjsslem eleq1i pjhcli
        hvsubcl pjoc2 bitr pjsub eqeq1i hvsubeq0 3bitr pjidm eqeq2i bitr2
        sylibr ) CBGZACHIZJIIZABHIZJIIZKLZVEMZABJIIZCJIZIZAVLIZNZVDVFVEMZVHVEMZ
        OZVJVDVQVPVDVGVEGZVQCBDFPVSVHVGMVQAVGBFQERVGVEVHSTUAAVECDQZERUBVEUCMVRV
        JUDVEVTUEVFVHVEUFUGUHVJVMVNVLIZNZVOVJVKVNKLZVLIZUINZVMWAKLZUINWBVJWCVEM
        WEVIWCVEABCDEFUJUKWCCDVKVNABFEULZACDEULZUMUNUOWDWFUIVKVNCDWGWHUPUQVMWAV
        KCDWGULVNCDWHULURUSWAVNVMACDEUTVAVBVC $.
        $( [31-Oct-1999] $)

      $( Projection meet property.  Remark in [Kalmbach] p. 66.   Also Theorem
         4.5(i)->(iv) of [Beran] p. 112. $)
      pjssm $p |- ( H (_ G -> ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) )
                  = ( ( proj ` ( G i^i ( _|_ ` H ) ) ) ` A ) ) $=
        ( wss cpj cfv cmv co cort cin wcel wa wceq pjcli ssel mpi jctil csh wi
        chshi shsubclt ax-mp syl chsscon3 choccl sylbi pjsslem syl5eqelr jca
        elin chincl pjhcli hvsubcl pjch bitr3 sylib c1 cneg csm cva pjsub
        hvsubval c0v inss1 pjss2 shococss inss2 mpbi sstri sselii pjoc2
        opreq2i cc ax1cn negcl hvmul0t eqtr opreq12i chil ax-hvaddid 3eqtr
        syl5reqr ) CBGZABHIIZACHIIZJKZBCLIZMZHIZIZWIAWLIZWFWIBNZWIWJNZOZWMWIPZW
        FWOWPWFWGBNZWHBNZOZWOWFWTWSWFWHCNWTACDEQZCBWHRSABFEQTBUANXAWOUBBFUCWGWH
        BUDUEUFWFAWJHIIZABLIZHIIZJKZWJWIWFXDWJGZXFWJNZCBDFUGXGXCWJNZXEWJNZOZXHX
        GXJXIXGXEXDNXJAXDBFUHEQXDWJXERSAWJCDUHZEQTWJUANXKXHUBWJXLUCXCXEWJUDUEUF
        UIABCDEFUJUKULWQWIWKNWRWIBWJUMWIWKBWJFXLUNZWGWHABFEUOZACDEUOZUPUQURUSWM
        WGWLIZWHWLIZJKXPUTVAZXQVBKZVCKZWNWGWHWKXMXNXOVDXPXQWGWKXMXNUOWHWKXMXOUO
        VEXTWNVFVCKZWNXPWNXSVFVCWKBGXPWNPBWJVGABWKXMEFVHUEXSXRVFVBKZVFXQVFXRVBW
        HWKLIZNXQVFPCYCWHCWJLIZYCCUANCYDGCDUCCVIUEWKWJGYDYCGBWJVJWKWJXMXLUGVKVL
        XBVMWHWKXMXOVNVKVOXRVPNYBVFPUTVQVRXRVSUEVTWAWNWBNYAWNPAWKXMEUOWNWCUEVTW
        DWE $.
        $( [31-Oct-1999] $)

      $( Theorem 4.5(iv)->(v) of [Beran] p. 112. $)
      pjssge0 $p |- ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) )
                    = ( ( proj ` ( G i^i ( _|_ ` H ) ) ) ` A ) ->
   0 <_ ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) .ih A ) ) $=
        ( cpj cfv cmv co cort cin wceq cno c2 cexp csp cc0 cle opreq1 choccl
        chincl pjinorm syl6eq pjhcli normcl sqge0 syl5breqr ) ABGHHACGHHIJZABCK
        HZLZGHHZMZULNHZOPJZUIAQJZRSUMUPULAQJUOUIULAQTAUKBUJFCDUAUBZEUCUDUNULAUK
        UQEUEUFUGUH $.
        $( [13-Aug-2000] $)

      $( Theorem 4.5(v)<->(vi) of [Beran] p. 112. $)
      pjdifnorm $p |- ( 0 <_
                 ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) .ih A ) <->
      ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` ( ( proj ` G ) ` A ) ) ) $=
        ( cc0 cpj cfv cno c2 cexp co cmin cle wbr cmv csp pjhcli normcl resqcl
        subge0 chil wcel wceq his2subt mp3an pjinorm opreq12i eqtr breq2i wb
        normge0t ax-mp le2sq mp2an 3bitr4 ) GABHIIZJIZKLMZACHIIZJIZKLMZNMZOPVCU
        TOPZGURVAQMARMZOPVBUSOPZUTVCUSURABFESZTZUAVBVAACDESZTZUAUBVFVDGOVFURARM
        ZVAARMZNMZVDURUCUDZVAUCUDZAUCUDVFVNUEVHVJEURVAAUFUGVLUTVMVCNABFEUHACDEU
        HUIUJUKGVBOPZGUSOPZVGVEULVPVQVJVAUMUNVOVRVHURUMUNVBUSVKVIUOUPUQ $.
        $( [13-Aug-2000] $)

      $( The projection on a subspace join is the sum of the projections. $)
      pjcj $p |- ( H (_ ( _|_ ` G ) -> ( ( proj ` ( H vH G ) ) ` A ) =
                ( ( ( proj ` H ) ` A ) +h ( ( proj ` G ) ` A ) ) ) $=
        ( cort cfv wss cpj cva co chj cmv cin choccl pjssm opreq2d c1 cneg csm
        chil wcel wceq pjhcli hvaddsub12t mp3an pjpo opreq2i hvnegdi 3eqtr4
        hvsubcl hvsubval eqtr4 chjcom chdmm4 fveq2i fveq1i chincl pjop eqtr
        3eqtr4g eqcomd ) CBGHZIZACJHHZABJHHZKLZACBMLZJHZHZVEAAVDJHHZVFNLZNLZAAV
        DCGHZOZJHHZNLZVHVKVEVMVQANAVDCDEBFPZQRVHASTVMUALZKLZVNVFAVLNLZKLZAVFVLN
        LZKLZVHWAVFUBUCAUBUCVLUBUCWCWEUDACDEUEZEAVDVSEUEZVFAVLUFUGVGWBVFKABFEUH
        UIVTWDAKVLVFWGWFUJUIUKAVMEVLVFWGWFULUMUNVKAVPGHZJHZHVRAVJWIVIWHJVIBCMLW
        HCBDFUOBCFDUPUNUQURAVPVDVOVSCDPUSEUTVAVBVC $.
        $( [1-Nov-1999] $)
    $}
  $}

  ${
    pjadjt.1 $e |- H e. CH $.
    $( A projection is self-adjoint.  Property (i) of [Beran] p. 109. $)
    pjadjt $p |- ( ( A e. H~ /\ B e. H~ ) -> ( ( ( proj ` H ) ` A ) .ih B ) =
                  ( A .ih ( ( proj ` H ) ` B ) ) ) $=
      ( chil wcel cpj cfv csp co wceq c0v cif fveq2 opreq1d opreq1 eqeq12d
      opreq2 opreq2d ax-hv0cl elimel pjadj dedth2h ) AEFZBEFZACGHZHZBIJZABUFHZI
      JZKUDALMZUFHZBIJZUKUIIJZKULUEBLMZIJZUKUOUFHZIJZKABLLAUKKZUHUMUJUNUSUGULBI
      AUKUFNOAUKUIIPQBUOKZUMUPUNURBUOULIRUTUIUQUKIBUOUFNSQUKUOCDALETUABLETUAUBU
      C $.
      $( [6-Oct-2000] $)

    $( Projection of vector sum is sum of projections. $)
    pjaddt $p |- ( ( A e. H~ /\ B e. H~ ) -> ( ( proj ` H ) ` ( A +h B ) ) =
                ( ( ( proj ` H ) ` A ) +h ( ( proj ` H ) ` B ) ) ) $=
      ( chil wcel cva co cpj cfv wceq c0v cif opreq1 fveq2d fveq2 opreq1d
      eqeq12d opreq2 opreq2d ax-hv0cl elimel pjadd dedth2h ) AEFZBEFZABGHZCIJZJ
      ZAUHJZBUHJZGHZKUEALMZBGHZUHJZUMUHJZUKGHZKUMUFBLMZGHZUHJZUPURUHJZGHZKABLLA
      UMKZUIUOULUQVCUGUNUHAUMBGNOVCUJUPUKGAUMUHPQRBURKZUOUTUQVBVDUNUSUHBURUMGSO
      VDUKVAUPGBURUHPTRUMURCDALEUAUBBLEUAUBUCUD $.
      $( [14-Nov-2000] $)

    $( The inner product of a projection and its argument is the square of the
       norm of the projection.  Remark in [Halmos] p. 44. $)
    pjinormt $p |- ( A e. H~ -> ( ( ( proj ` H ) ` A ) .ih A ) =
                 ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) ) $=
      ( chil wcel cpj cfv csp co cno c2 cexp wceq c0v cif fveq2 id opreq12d
      fveq2d opreq1d eqeq12d ax-hv0cl elimel pjinorm dedth ) ADEZABFGZGZAHIZUHJ
      GZKLIZMUFANOZUGGZULHIZUMJGZKLIZMANAULMZUIUNUKUPUQUHUMAULHAULUGPZUQQRUQUJU
      OKLUQUHUMJURSTUAULBCANDUBUCUDUE $.
      $( [28-Jun-2006] $) $( [2-Jun-2006] $)

    $( Projection of vector difference is difference of projections. $)
    pjsubt $p |- ( ( A e. H~ /\ B e. H~ ) -> ( ( proj ` H ) ` ( A -h B ) ) =
                ( ( ( proj ` H ) ` A ) -h ( ( proj ` H ) ` B ) ) ) $=
      ( chil wcel cmv co cpj cfv wceq c0v cif opreq1 fveq2d fveq2 opreq1d
      eqeq12d opreq2 opreq2d ax-hv0cl elimel pjsub dedth2h ) AEFZBEFZABGHZCIJZJ
      ZAUHJZBUHJZGHZKUEALMZBGHZUHJZUMUHJZUKGHZKUMUFBLMZGHZUHJZUPURUHJZGHZKABLLA
      UMKZUIUOULUQVCUGUNUHAUMBGNOVCUJUPUKGAUMUHPQRBURKZUOUTUQVBVDUNUSUHBURUMGSO
      VDUKVAUPGBURUHPTRUMURCDALEUAUBBLEUAUBUCUD $.
      $( [14-Nov-2000] $)

    $( Projection of scalar product is scalar product of projection. $)
    pjmult $p |- ( ( A e. CC /\ B e. H~ ) -> ( ( proj ` H ) ` ( A .h B ) ) =
                ( A .h ( ( proj ` H ) ` B ) ) ) $=
      ( cc wcel chil csm co cpj cfv wceq cc0 cif c0v opreq1 fveq2d eqeq12d
      opreq2 fveq2 opreq2d ax-hv0cl elimel 0cn pjmul dedth2h ) AEFZBGFZABHIZCJK
      ZKZABUJKZHIZLUGAMNZBHIZUJKZUNULHIZLUNUHBONZHIZUJKZUNURUJKZHIZLABMOAUNLZUK
      UPUMUQVCUIUOUJAUNBHPQAUNULHPRBURLZUPUTUQVBVDUOUSUJBURUNHSQVDULVAUNHBURUJT
      UARURUNCDBOGUBUCAMEUDUCUEUF $.
      $( [26-Nov-2000] $)

    $( The inner product of a projection and its argument is nonnegative. $)
    pjige0 $p |- ( A e. H~ -> 0 <_ ( ( ( proj ` H ) ` A ) .ih A ) ) $=
      ( chil wcel cc0 cpj cfv cno c2 cexp co csp cle cr wbr pjhcl normclt
      sqge0t 3syl pjinormt breqtrrd ) ADEZFABGHHZIHZJKLZUDAMLNUCUDDEUEOEFUFNPAB
      CQUDRUESTABCUAUB $.
      $( [2-Jul-2006] $) $( [2-Jun-2006] $)
  $}

  $( The inner product of a projection and its argument is nonnegative. $)
  pjige0t $p |- ( ( H e. CH /\ A e. H~ ) ->
                0 <_ ( ( ( proj ` H ) ` A ) .ih A ) ) $=
    ( cch wcel chil cc0 cpj cfv csp co cle wbr wi c0h cif wceq fveq2 fveq1d
    opreq1d breq2d imbi2d h0elch elimel pjige0 dedth imp ) BCDZAEDZFABGHZHZAIJZ
    KLZUGUHULMUHFAUGBNOZGHZHZAIJZKLZMBNBUMPZULUQUHURUKUPFKURUJUOAIURAUIUNBUMGQR
    STUAAUMBNCUBUCUDUEUF $.
    $( [4-Jul-2006] $) $( [2-Jun-2006] $)

  $( The projection on a subspace join is the sum of the projections. $)
  pjcjt2 $p |- ( ( H e. CH /\ G e. CH /\ A e. H~ ) ->
            ( H (_ ( _|_ ` G ) -> ( ( proj ` ( H vH G ) ) ` A ) =
            ( ( ( proj ` H ) ` A ) +h ( ( proj ` G ) ` A ) ) ) ) $=
    ( cch wcel chil cort cfv wss chj co cpj cva wceq wi cif c0v sseq1 opreq1
    fveq2d fveq1d fveq2 opreq1d eqeq12d imbi12d sseq2d opreq2 opreq2d opreq12d
    imbi2d helch elimel ax-hv0cl pjcj dedth3h ) CDEZBDEZAFEZCBGHZIZACBJKZLHZHZA
    CLHZHZABLHZHZMKZNZOUPCFPZUSIZAVJBJKZLHZHZAVJLHZHZVGMKZNZOCBAFFQVJUQBFPZGHZI
    ZAVJVSJKZLHZHZVPAVSLHZHZMKZNZOWAURAQPZWCHZWIVOHZWIWEHZMKZNZOCVJNZUTVKVIVRCV
    JUSRWOVCVNVHVQWOAVBVMWOVAVLLCVJBJSTUAWOVEVPVGMWOAVDVOCVJLUBUAUCUDUEBVSNZVKW
    AVRWHWPUSVTVJBVSGUBUFWPVNWDVQWGWPAVMWCWPVLWBLBVSVJJUGTUAWPVGWFVPMWPAVFWEBVS
    LUBUAUHUDUEAWINZWHWNWAWQWDWJWGWMAWIWCUBWQVPWKWFWLMAWIVOUBAWIWEUBUIUDUJWIVSV
    JCFDUKULAQFUMULBFDUKULUNUO $.
    $( [1-Nov-1999] $)

  ${
    pj0.1 $e |- H e. CH $.
    $( The projection of the zero vector. $)
    pj0 $p |- ( ( proj ` H ) ` 0h ) = 0h $=
      ( c0v cort cfv wcel cpj wceq csh chshi oc0 ax-mp ax-hv0cl pjoc2 mpbi ) CA
      DEFZCAGEECHAIFPABJAKLCABMNO $.
      $( [31-Oct-1999] $)
  $}

  $( Projection of a vector in the projection subspace.  Lemma 4.4(ii) of
     [Beran] p. 111. $)
  pjcht $p |- ( ( H e. CH /\ A e. H~ ) ->
              ( A e. H <-> ( ( proj ` H ) ` A ) = A ) ) $=
    ( cch wcel chil cpj cfv wceq wb cif c0v eleq2 fveq2 fveq1d eqeq1d bibi12d
    eleq1 id eqeq12d helch elimel ax-hv0cl pjch dedth2h ) BCDZAEDZABDZABFGZGZAH
    ZIAUEBEJZDZAUKFGZGZAHZIUFAKJZUKDZUPUMGZUPHZIBAEKBUKHZUGULUJUOBUKALUTUIUNAUT
    AUHUMBUKFMNOPAUPHZULUQUOUSAUPUKQVAUNURAUPAUPUMMVARSPUPUKBECTUAAKEUBUAUCUD
    $.
    $( [30-Oct-1999] $)

  $( The projection of a vector in the projection subspace is itself. $)
  pjidt $p |- ( ( H e. CH /\ A e. H ) -> ( ( proj ` H ) ` A ) = A ) $=
    ( cch wcel chil wa cpj cfv wceq pjcht biimpa pm3.26 chelt jca pm3.27
    sylanc ) BCDZAEDZFZABDZABGHHAIZQTFZSTUAABJKUBQRQTLABMNQTOP $.
    $( [9-Apr-2006] $) $( [9-Apr-2006] $)

  ${
    $d x H $.
    $( The set of vectors belonging to the subspace of a projection.  Part of
       Theorem 26.2 of [Halmos] p. 44. $)
    pjvect $p |- ( H e. CH -> H = { x e. H~ | ( ( proj ` H ) ` x ) = x } ) $=
      ( cch wcel chil cin cv cpj cfv wceq crab wss chss sseqin2 sylib wb pjcht
      ex rabbirdv eqtr3d ) BCDZEBFZBAGZBHIIUCJZAEKUABELUBBJBMBENOUAUDABEUAUCEDU
      CBDUDPUCBQRST $.
      $( [13-Oct-2006] $) $( [11-Apr-2006] $)

    $( The set of vectors belonging to the orthocomplemented subspace of a
       projection.  Second part of Theorem 27.3 of [Halmos] p. 45. $)
    pjocvect $p |- ( H e. CH -> ( _|_ ` H ) =
                   { x e. H~ | ( ( proj ` H ) ` x ) = 0h } ) $=
      ( cch wcel chil cort cfv cin cv cpj c0v wceq crab wss chocclt chss syl
      sseqin2 sylib wb pjoc2t ex rabbirdv eqtr3d ) BCDZEBFGZHZUFAIZBJGGKLZAEMUE
      UFENZUGUFLUEUFCDUJBOUFPQUFERSUEUIAUFEUEUHEDUHUFDUITUHBUAUBUCUD $.
      $( [14-Oct-2006] $) $( [24-Apr-2006] $)
  $}

  ${
    pjocin.1 $e |- G e. CH $.
    pjocin.2 $e |- H e. CH $.
    $( Membership of projection in orthocomplement of intersection. $)
    pjocin $p |- ( A e. ( _|_ ` ( G i^i H ) ) ->
                 ( ( proj ` G ) ` A ) e. ( _|_ ` ( G i^i H ) ) ) $=
      ( cin cort cfv wcel cpj cmv co chil wceq chincl choccl chel cch pjpot
      mpan syl pjcl wss inss1 chsscon3 mpbi sseli 3syl csh wa wi chshi
      shsubclt ax-mp mpdan eqeltrd ) ABCFZGHZIZABJHHZAABGHZJHHZKLZURUSAMIZUTVCN
      ZAURUQBCDEOZPZQZBRIVDVEDABSTUAUSVBURIZVCURIZUSVDVBVAIVIVHAVABDPUBVAURVBUQ
      BUCVAURUCBCUDUQBVFDUEUFUGUHURUIIUSVIUJVJUKURVGULAVBURUMUNUOUP $.
      $( [21-Apr-2001] $)

    $( Membership of projection in an intersection. $)
    pjin $p |- ( A e. ( G i^i H ) ->
                 ( ( proj ` G ) ` A ) e. ( G i^i H ) ) $=
      ( cin wcel cpj cfv wceq inss1 sseli cch pjidt mpan syl eleq1d ibir ) ABCF
      ZGZABHIIZSGTUAASTABGZUAAJZSBABCKLBMGUBUCDABNOPQR $.
      $( [22-Apr-2001] $)
  $}

  ${
    $d x y z w G $.  $d x y z w H $.
    pjjs.1 $e |- G e. CH $.
    pjjs.2 $e |- H e. SH $.
    $( A sufficient condition for subspace join to be equal to subspace sum. $)
    pjjs $p |- ( A. x e. ( G vH H ) ( ( proj ` ( _|_ ` G ) ) ` x ) e. H ->
               ( G vH H ) = ( G +H H ) ) $=
      ( vw vy vz cv cort cfv cpj wcel chj co wral cph wss wa wceq fveq2 eleq1d
      rcla4v cva wrex chil pjcl anim1i cch axpjpjt mpan adantr jca chshi shjcl
      chel sylan rcla4eopr 3expa syl shsel sylibr ex syld com12 ssrdv shslej
      jctir eqss ) AIZBJKLKZKZCMZABCNOZPZVNBCQOZRZVPVNRZSVNVPTVOVQVRVOFVNVPFIZV
      NMZVOVSVPMZVTVOVSVKKZCMZWAVMWCAVSVNVJVSTVLWBCVJVSVKUAUBUCVTWCWAVTWCSZVSGI
      HIUDOTHCUEGBUEZWAWDVSBLKKZBMZWCSZVSWFWBUDOTZSZWEVSUFMZWCWJVTWKWCSWHWIWKWG
      WCVSBDUGUHWKWIWCBUIMWKWIDVSBUJUKULUMVSVNBCBDUNZEUOUPUQWGWCWIWEGHBCWFWBVSU
      DURUSUTGHBCVSWLEVAVBVCVDVEVFBCWLEVGVHVNVPVIVB $.
      $( [29-Aug-2004] $) $( [29-May-2004] $)
  $}

  ${
    $d x y z w H $.
    pjfn.1 $e |- H e. CH $.
    $( Functionality of a projection. $)
    pjfn $p |- ( proj ` H ) Fn H~ $=
      ( vx vy vz vw chil cv cva co wceq cort cfv wrex crab cuni cpj cch
      elisseti rabex uniex wcel wa copab pjmvalt ax-mp fnopab2 ) CDGCHZEHFHIJKF
      ALMNZEAOZPZAQMZUJUIEAARBSTUAARUBULUHGUBDHUKKUCCDUDKBCDEFAUEUFUG $.
      $( [30-Oct-1999] $)

    $( The range of a projection.  Part of Theorem 26.2 of [Halmos] p. 44. $)
    pjrn $p |- ran ( proj ` H ) = H $=
      ( vx vy cv chil wcel cpj cfv wceq wa copab crn wex cab rnopab wfn pjfn
      fnopabfv mpbi rneqi visset eleq1 fveq2 eqeq2d anbi12d cla4ev chel cch
      pjidt mpan eqcomd sylanc pjcl syl5bir impcom 19.23aiv impbi abbi2i
      3eqtr4 ) CEZFGZDEZVAAHIZIZJZKZCDLZMVGCNZDOVDMAVGCDPVDVHVDFQVDVHJABRCDFVDS
      TUAVIDAVCAGZVIVCFGZVCVCVDIZJZVIVJVGVKVMKCVCDUBVAVCJZVBVKVFVMVAVCFUCVNVEVL
      VCVAVCVDUDUEUFUGVCABUHVJVLVCAUIGVJVLVCJBVCAUJUKULUMVGVJCVFVBVJVFVJVEAGVBV
      CVEAUCVAABUNUOUPUQURUSUT $.
      $( [30-Oct-1999] $)

    $( A projection maps onto its subspace. $)
    pjfo $p |- ( proj ` H ) : H~ -onto-> H $=
      ( chil cpj cfv wfo wfn crn wceq df-fo pjfn pjrn mpbir2an ) CAADEZFNCGNHAI
      CANJABKABLM $.
      $( [17-May-2006] $) $( [24-Apr-2006] $)

    $( The mapping of a projection. $)
    pjf $p |- ( proj ` H ) : H~ --> H~ $=
      ( chil cpj cfv wf wfn crn wss df-f pjfn pjrn chssi eqsstr mpbir2an ) CCAD
      EZFPCGPHZCICCPJABKQACABLABMNO $.
      $( [11-Nov-2000] $)

    $( The value of a projection in terms of components. $)
    pjv $p |- ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
               ( ( proj ` H ) ` ( A +h B ) ) = A ) $=
      ( wcel cort cfv wa cva co cpj c0v chil wceq pjaddt chel choccl syl2an
      cch pjidt mpan wb pjoc2tOLD syl ibi opreqan12d ax-hvaddid adantr 3eqtrd
      ) ACEZBCFGZEZHABIJCKGZGZAUMGZBUMGZIJZALIJZAAMEZBMEZUNUQNUJULABCDOACDPZBUK
      CDQPZRUJULUOAUPLICSEUJUOANDACTUAULUPLNZULUTULVCUBVBBCDUCUDUEUFUJURANZULUJ
      USVDVAAUGUDUHUI $.
      $( [28-Nov-2000] $)
  $}

  $( A projection maps onto its subspace. $)
  pjfot $p |- ( H e. CH -> ( proj ` H ) : H~ -onto-> H ) $=
    ( cch wcel chil cpj cfv wfo c0h cif wceq wb fveq2 foeq1 syl foeq3 h0elch
    elimel pjfo dedth2v ) ABCZDAAEFZGZDATAHIZEFZGZDUCUDGAAHHAUCJUAUDJUBUEKAUCEL
    DAUAUDMNAUCDUDOUCAHBPQRS $.
    $( [17-May-2006] $) $( [24-Apr-2006] $)

  $( The range of a projection.  Part of Theorem 26.2 of [Halmos] p. 44. $)
  pjrnt $p |- ( H e. CH -> ran ( proj ` H ) = H ) $=
    ( cch wcel chil cpj cfv wfo crn wceq pjfot forn syl ) ABCDAAEFZGMHAIAJDAMKL
    $.
    $( [20-May-2006] $) $( [24-Apr-2006] $)

  $( The mapping of a projection. $)
  pjft $p |- ( H e. CH -> ( proj ` H ) : H~ --> H~ ) $=
    ( chil cpj cfv wf wss cch wcel fss wfo pjfot fof syl chss sylanc ) BAACDZEZ
    ABFBBPEAGHZBABPIRBAPJQAKBAPLMANO $.
    $( [22-May-2006] $) $( [24-Apr-2006] $)

  $( Functionality of a projection. $)
  pjfnt $p |- ( H e. CH -> ( proj ` H ) Fn H~ ) $=
    ( cch wcel chil cpj cfv wf wfn pjft ffn syl ) ABCDDAEFZGLDHAIDDLJK $.
    $( [1-Jul-2006] $) $( [30-May-2006] $)

  ${
    pjsumt.1 $e |- G e. CH $.
    pjsumt.2 $e |- H e. CH $.
    $( The projection on a subspace sum is the sum of the projections. $)
    pjsumt $p |- ( A e. H~ ->
            ( G (_ ( _|_ ` H ) -> ( ( proj ` ( G +H H ) ) ` A ) =
            ( ( ( proj ` G ) ` A ) +h ( ( proj ` H ) ` A ) ) ) ) $=
      ( chil wcel cort cfv wss cph co cpj cva wceq wa chj osum fveq2d fveq1d
      adantl cch wi pjcjt2 mp3an12 imp eqtrd ex ) AFGZBCHIJZABCKLZMIZIZABMIIACM
      IINLZOUIUJPUMABCQLZMIZIZUNUJUMUQOUIUJAULUPUJUKUOMBCDERSTUAUIUJUQUNOZBUBGC
      UBGUIUJURUCDEACBUDUEUFUGUH $.
      $( [11-Nov-2000] $)

    $( One-to-one correspondence of projection and subspace. $)
    pj11 $p |- ( ( proj ` G ) = ( proj ` H ) <-> G = H ) $=
      ( cpj cfv wceq crn rneq pjrn 3eqtr3g fveq2 impbi ) AEFZBEFZGZABGPNHOHABNO
      IACJBDJKABELM $.
      $( [26-Nov-2000] $)

    $( Vector decomposition into sum of projections on orthogonal subspaces. $)
    pjds $p |- ( ( A e. ( G vH H ) /\ G (_ ( _|_ ` H ) ) ->
               A = ( ( ( proj ` G ) ` A ) +h ( ( proj ` H ) ` A ) ) ) $=
      ( chj co wcel cort cfv wss wa cph cpj cva osum fveq2d fveq1d cch wceq
      chjcl pjidt mpan sylan9eqr chil pjsumt imp chel sylan eqtr3d ) ABCFGZHZBC
      IJKZLABCMGZNJZJZAABNJJACNJJOGZUMULUPAUKNJZJZAUMAUOURUMUNUKNBCDEPQRUKSHULU
      SATBCDEUAZAUKUBUCUDAUEHZUMUPUQTZULVAUMVBABCDEUFUGAUKUTUHUIUJ $.
      $( [22-Jun-2006] $) $( [21-Jun-2006] $)
  $}

  ${
    pjds3.1 $e |- F e. CH $.
    pjds3.2 $e |- G e. CH $.
    pjds3.3 $e |- H e. CH $.
    $( Vector decomposition into sum of projections on orthogonal subspaces. $)
    pjds3 $p |- ( ( ( A e.  ( ( F vH G ) vH H ) /\ F (_ ( _|_ ` G ) ) /\
     ( F (_ ( _|_ ` H ) /\ G (_ ( _|_ ` H ) ) ) -> A = ( ( ( ( proj ` F ) ` A )
                +h ( ( proj ` G ) ` A ) ) +h ( ( proj ` H ) ` A ) ) ) $=
      ( chj co wcel cort cfv wss wa cpj cva cph wceq chjcl pjds pm3.26 choccl
      chlubi syl2an osum fveq2d fveq1d opreq1d ad2antlr chil pjsumt imp chel
      sylan adantr 3eqtr2d ) ABCHIZDHIZJZBCKLMZNZBDKLZMCVBMNZNAAUQOLZLZADOLLZPI
      ZABCQIZOLZLZVFPIZABOLLACOLLPIZVFPIZUSUQVBMAVGRVAVCAUQDBCEFSZGTUSUTUABCVBE
      FDGUBUCUDUTVKVGRUSVCUTVJVEVFPUTAVIVDUTVHUQOBCEFUEUFUGUHUIVAVKVMRVCVAVJVLV
      FPAUJJZUTVJVLRZUSVOUTVPABCEFUKULAURUQDVNGSUMUNUHUOUP $.
      $( [22-Jun-2006] $) $( [22-Jun-2006] $)
  $}

  $( One-to-one correspondence of projection and subspace. $)
  pj11t $p |- ( ( G e. CH /\ H e. CH ) ->
              ( ( proj ` G ) = ( proj ` H ) <-> G = H ) ) $=
    ( cch wcel cpj cfv wceq wb c0h cif fveq2 eqeq1d eqeq1 bibi12d eqeq2d eqeq2
    h0elch elimel pj11 dedth2h ) ACDZBCDZAEFZBEFZGZABGZHUAAIJZEFZUDGZUGBGZHUHUB
    BIJZEFZGZUGUKGZHABIIAUGGZUEUIUFUJUOUCUHUDAUGEKLAUGBMNBUKGZUIUMUJUNUPUDULUHB
    UKEKOBUKUGPNUGUKAICQRBICQRST $.
    $( [23-May-2006] $) $( [24-Apr-2006] $)

  ${
    $d f h w x y z $.
    $( Functionality of the projection function. $)
    pjmfn $p |- proj Fn CH $=
      ( vh vf vx vy vz vw cch cv chil wcel cva co wceq cort cfv wrex crab cuni
      wa copab cpj ax-hilex funopabex2 df-pj fnopab2 ) ABGCHZIJDHUFEHFHKLMFAHZN
      OPEUGQRZMSCDTUACDIUHUBUCCDEFBAUDUE $.
      $( [26-May-2006] $) $( [24-Apr-2006] $)

    $( The projector function maps one-to-one into the set of Hilbert space
       operators. $)
    pjmf1 $p |- proj : CH -1-1-> ( H~ ^m H~ ) $=
      ( vx vy cch chil cm co cpj wf1 wf cv cfv wceq wi wral f1fv wfn wcel
      ffnfv pjmfn pjft ax-hilex elmap sylibr rgen mpbir2an wa pj11t biimpd
      rgen2 ) CDDEFZGHCUJGIZAJZGKZBJZGKLZULUNLZMZBCNACNABCUJGOUKGCPUMUJQZACNACU
      JGRSURACULCQZDDUMIURULTDDUMUAUAUBUCUDUEUQABCUSUNCQUFUOUPULUNUGUHUIUE $.
      $( [14-Oct-2006] $) $( [24-Apr-2006] $)
  $}

  $( The inner product of projections on orthogonal subspaces vanishes. $)
  pjoi0t $p |- ( ( ( G e. CH /\ H e. CH /\ A e. H~ )  /\ G (_ ( _|_ ` H ) ) ->
               ( ( ( proj ` G ) ` A ) .ih ( ( proj ` H ) ` A ) ) = 0 ) $=
    ( cch wcel chil w3a cort cfv wss cpj crn csp co cc0 wceq wa pjrnt adantr
    fveq2d adantl sseq12d biimpar 3adantl3 csh shorth 3imp id eqeltrd chsh syl
    3ad2ant2 pm3.27 wfn fnfvrn pjfnt sylan 3adant2 3adant1 jca syl3anc syldan
    ) BDEZCDEZAFEZGZBCHIZJZBKIZLZCKIZLZHIZJZAVIIZAVKIZMNOPZVCVDVHVNVEVCVDQZVNVH
    VRVJBVMVGVCVJBPVDBRSVDVMVGPVCVDVLCHCRZTUAUBUCUDVLUEEZVNVOVJEZVPVLEZQZVQVFVN
    QVTVNWCVQVOVPVJVLUFUGVFVTVNVDVCVTVEVDVLDEVTVDVLCDVSVDUHUIVLUJUKULSVFVNUMVFW
    CVNVFWAWBVCVEWAVDVIFUNVEWAVCFAVIUOBUPUQURVDVEWBVCVKFUNVEWBVDFAVKUOCUPUQUSUT
    SVAVB $.
    $( [1-Jul-2006] $) $( [30-Jun-2006] $)

  ${
    pjoi0.1 $e |- G e. CH $.
    pjoi0.2 $e |- H e. CH $.
    pjoi0.3 $e |- A e. H~ $.
    $( The inner product of projections on orthogonal subspaces vanishes. $)
    pjoi0 $p |- ( G (_ ( _|_ ` H ) ->
                ( ( ( proj ` G ) ` A ) .ih ( ( proj ` H ) ` A ) ) = 0 ) $=
      ( cch wcel chil w3a cort cfv wss cpj csp co cc0 wceq 3pm3.2i pjoi0t mpan
      ) BGHZCGHZAIHZJBCKLMABNLLACNLLOPQRUBUCUDDEFSABCTUA $.
      $( [1-Nov-1999] $)

    $( Pythagorean theorem for projections on orthogonal subspaces. $)
    pjopyth $p |- ( G (_ ( _|_ ` H ) ->
       ( ( normh ` ( ( ( proj ` G ) ` A ) +h ( ( proj ` H ) ` A ) ) ) ^ 2 ) =
                 ( ( ( normh ` ( ( proj ` G ) ` A ) ) ^ 2 ) +
                   ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) ) ) $=
      ( cort cfv wss cpj csp co cc0 wceq cva cno c2 cexp caddc pjoi0 pjhcli
      normpyth syl ) BCGHIABJHHZACJHHZKLMNUDUEOLPHQRLUDPHQRLUEPHQRLSLNABCDEFTUD
      UEABDFUAACEFUAUBUC $.
      $( [1-Nov-1999] $)
  $}

  $( Pythagorean theorem for projections on orthogonal subspaces. $)
  pjopytht $p |- ( ( H e. CH /\ G e. CH /\ A e. H~ ) -> ( H (_ ( _|_ ` G ) ->
       ( ( normh ` ( ( ( proj ` H ) ` A ) +h ( ( proj ` G ) ` A ) ) ) ^ 2 ) =
                 ( ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) +
                   ( ( normh ` ( ( proj ` G ) ` A ) ) ^ 2 ) ) ) ) $=
    ( cch wcel chil cort cfv wss cpj cva co cno c2 cexp caddc wceq wi cif c0v
    sseq1 fveq2 fveq1d opreq1d fveq2d eqeq12d imbi12d sseq2d opreq2d opreq12d
    imbi2d helch elimel ax-hv0cl pjopyth dedth3h ) CDEZBDEZAFEZCBGHZIZACJHZHZAB
    JHZHZKLZMHZNOLZVCMHZNOLZVEMHZNOLZPLZQZRUQCFSZUTIZAVOJHZHZVEKLZMHZNOLZVRMHZN
    OLZVLPLZQZRCBAFFTVOURBFSZGHZIZVRAWFJHZHZKLZMHZNOLZWCWJMHZNOLZPLZQZRWHUSATSZ
    VQHZWRWIHZKLZMHZNOLZWSMHZNOLZWTMHZNOLZPLZQZRCVOQZVAVPVNWECVOUTUAXJVHWAVMWDX
    JVGVTNOXJVFVSMXJVCVRVEKXJAVBVQCVOJUBUCZUDUEUDXJVJWCVLPXJVIWBNOXJVCVRMXKUEUD
    UDUFUGBWFQZVPWHWEWQXLUTWGVOBWFGUBUHXLWAWMWDWPXLVTWLNOXLVSWKMXLVEWJVRKXLAVDW
    IBWFJUBUCZUIUEUDXLVLWOWCPXLVKWNNOXLVEWJMXMUEUDUIUFUGAWRQZWQXIWHXNWMXCWPXHXN
    WLXBNOXNWKXAMXNVRWSWJWTKAWRVQUBZAWRWIUBZUJUEUDXNWCXEWOXGPXNWBXDNOXNVRWSMXOU
    EUDXNWNXFNOXNWJWTMXPUEUDUJUFUKWRVOWFCFDULUMBFDULUMATFUNUMUOUP $.
    $( [2-Nov-1999] $)

  ${
    pjnorm.1 $e |- H e. CH $.
    pjnorm.2 $e |- A e. H~ $.
    $( The norm of the projection is less than or equal to the norm. $)
    pjnorm $p |- ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` A ) $=
      ( cpj cfv cno cort cva co cle chil wcel wa csp cc0 wceq wbr pjhcli
      choccl pm3.2i cch pjorth ax-mp normpyct mp2 pjpj fveq2i breqtrr ) ABEFFZG
      FZUJABHFZEFFZIJZGFZAGFKUJLMZUMLMZNUJUMOJPQZUKUOKRUPUQABCDSAULBCTDSUABUBMU
      RCAABDDUCUDUJUMUEUFAUNGABCDUGUHUI $.
      $( [27-Oct-1999] $)

    $( Pythagorean theorem for projections. $)
    pjpyth $p |- ( ( normh ` A ) ^ 2 ) =
                 ( ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) +
                   ( ( normh ` ( ( proj ` ( _|_ ` H ) ) ` A ) ) ^ 2 ) ) $=
      ( cno cfv c2 cexp co cpj cort cva caddc pjpj fveq2i opreq1i wss wceq csh
      wcel chshi shococss ax-mp choccl pjopyth eqtr ) AEFZGHIABJFFZABKFZJFFZLIZ
      EFZGHIZUHEFGHIUJEFGHIMIZUGULGHAUKEABCDNOPBUIKFQZUMUNRBSTUOBCUABUBUCABUICB
      CUDDUEUCUF $.
      $( [27-Oct-1999] $)

    $( If a vector does not belong to subspace, the norm of its projection
       is less than its norm. $)
    pjnel $p |- ( -. A e. H <->
                ( normh ` ( ( proj ` H ) ` A ) ) < ( normh ` A ) ) $=
      ( cpj cfv cno wceq wn cle wbr wa wcel clt pjnorm biantrur cort c0v c2
      cexp co pjoc1 cc0 caddc pjhcli normcl resqcl recn 0cn sqcl choccl addcan
      sq0 opreq2i addid1 eqtr2 pjpyth eqeq12i wb 0re leid chil normge0t ax-mp
      sq11 mp2an eqcom norm-i 3bitr2r 3bitr4r 3bitr negbii ltlen 3bitr4 ) ABEFF
      ZGFZAGFZHZIZVPVQJKZVSLABMZIVPVQNKVTVSABCDOPWAVRWAABQFZEFFZRHZVPSTUAZVQSTU
      AZHZVRABCDUBWEUCSTUAZUDUAZWEWCGFZSTUAZUDUAZHWHWKHZWGWDWEWHWKWEVPVOABCDUEZ
      UFZUGUHZUCUIUJWKWJWCAWBBCUKDUEZUFZUGUHULWEWIWFWLWIWEUCUDUAWEWHUCWEUDUMUNW
      EWPUOUPABCDUQURWMUCWJHZWJUCHWDUCUCJKUCWJJKZWMWSUSUCUTVAWCVBMWTWQWCVCVDUCW
      JUTWRVEVFWJUCVGWCWQVHVIVJUCVPJKZUCVQJKZWGVRUSVOVBMXAWNVOVCVDAVBMXBDAVCVDV
      PVQWOADUFZVEVFVKVLVPVQWOXCVMVN $.
      $( [27-Oct-1999] $)
  $}

  $( The norm of the projection is less than or equal to the norm. $)
  pjnormt $p |- ( ( H e. CH /\ A e. H~ ) ->
                ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` A ) ) $=
    ( cch wcel chil cpj cfv cno cle wbr cif c0v wceq fveq2 fveq1d fveq2d
    breq1d breq12d helch elimel ax-hv0cl pjnorm dedth2h ) BCDZAEDZABFGZGZHGZAHG
    ZIJAUDBEKZFGZGZHGZUIIJUEALKZUKGZHGZUNHGZIJBAELBUJMZUHUMUIIURUGULHURAUFUKBUJ
    FNOPQAUNMZUMUPUIUQIUSULUOHAUNUKNPAUNHNRUNUJBECSTALEUATUBUC $.
    $( [28-Oct-1999] $)

  $( Pythagorean theorem for projectors. $)
  pjpytht $p |- ( ( H e. CH /\ A e. H~ ) -> ( ( normh ` A ) ^ 2 ) =
               ( ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) +
                 ( ( normh ` ( ( proj ` ( _|_ ` H ) ) ` A ) ) ^ 2 ) ) ) $=
    ( cch wcel chil cno cfv c2 cexp co cpj cort caddc wceq cif c0v fveq2
    fveq1d fveq2d opreq1d opreq12d eqeq2d eqeq12d helch elimel ax-hv0cl pjpyth
    dedth2h ) BCDZAEDZAFGZHIJZABKGZGZFGZHIJZABLGZKGZGZFGZHIJZMJZNULAUIBEOZKGZGZ
    FGZHIJZAVCLGZKGZGZFGZHIJZMJZNUJAPOZFGZHIJZVNVDGZFGZHIJZVNVIGZFGZHIJZMJZNBAE
    PBVCNZVBVMULWDUPVGVAVLMWDUOVFHIWDUNVEFWDAUMVDBVCKQRSTWDUTVKHIWDUSVJFWDAURVI
    WDUQVHKBVCLQSRSTUAUBAVNNZULVPVMWCWEUKVOHIAVNFQTWEVGVSVLWBMWEVFVRHIWEVEVQFAV
    NVDQSTWEVKWAHIWEVJVTFAVNVIQSTUAUCVNVCBECUDUEAPEUFUEUGUH $.
    $( [25-Sep-2006] $) $( [11-Apr-2006] $)

  $( If a vector does not belong to subspace, the norm of its projection
     is less than its norm. $)
  pjnelt $p |- ( ( H e. CH /\ A e. H~ ) -> ( -. A e. H <->
              ( normh ` ( ( proj ` H ) ` A ) ) < ( normh ` A ) ) ) $=
    ( cch wcel chil wn cpj cfv cno clt wbr wb cif c0v wceq eleq2 negbid fveq2
    fveq1d fveq2d breq1d bibi12d eleq1 breq12d helch elimel ax-hv0cl pjnel
    dedth2h ) BCDZAEDZABDZFZABGHZHZIHZAIHZJKZLAUJBEMZDZFZAUSGHZHZIHZUQJKZLUKANM
    ZUSDZFZVFVBHZIHZVFIHZJKZLBAENBUSOZUMVAURVEVMULUTBUSAPQVMUPVDUQJVMUOVCIVMAUN
    VBBUSGRSTUAUBAVFOZVAVHVEVLVNUTVGAVFUSUCQVNVDVJUQVKJVNVCVIIAVFVBRTAVFIRUDUBV
    FUSBECUEUFANEUGUFUHUI $.
    $( [2-Nov-1999] $)

  $( A vector belongs to the subspace of a projection iff the norm of its
     projection equals its norm.  This and ~ pjcht yield Theorem 26.3 of
     [Halmos] p. 44. $)
  pjnorm2t $p |- ( ( H e. CH /\ A e. H~ ) -> ( A e. H <->
              ( normh ` ( ( proj ` H ) ` A ) ) = ( normh ` A ) ) ) $=
    ( cch wcel chil wa cpj cfv cno wceq cle wbr clt wn cr wb eqleltt pjhclt
    normclt syl adantl sylanc pjnormt biantrurd pjnelt con1bid 3bitr2rd ) BCDZA
    EDZFZABGHHZIHZAIHZJZULUMKLZULUMMLZNZFZUQABDZULODZUMODZUNURPUJULUMQUJUKEDUTA
    BRUKSTUIVAUHASUAUBUJUOUQABUCUDUJUSUPABUEUFUG $.
    $( [7-Apr-2001] $)

