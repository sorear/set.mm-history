$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Projectors (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    pjorth.1 $e |- A e. ~H $.
    pjorth.2 $e |- B e. ~H $.
    $( Projection components on orthocomplemented subspaces are orthogonal. $)
    pjorthi $p |- ( H e. CH ->
          ( ( ( proj ` H ) ` A ) .ih ( ( proj ` ( _|_ ` H ) ) ` B ) ) = 0 ) $=
      ( cch wcel csh cpj cfv cort wa csp co cc0 wceq chsh chil axpjcl mpan2
      choccl syl jca shocorth sylc ) CFGZCHGACIJJZCGZBCKJZIJJZUIGZLUGUJMNOPCQUF
      UHUKUFARGUHDACSTUFUIFGZUKCUAULBRGUKEBUISTUBUCUGUJCUDUE $.
      $( [26-Oct-1999] $)
  $}

  $( Property of identity projection.  Remark in [Beran] p. 111. $)
  pjch1 $p |- ( A e. ~H -> ( ( proj ` ~H ) ` A ) = A ) $=
    ( chil wcel cpj cfv wceq wb c0v cif eleq1 fveq2 id eqeq12d bibi12d helch
    ax-hv0cl elimel pjchi dedth ibi ) ABCZABDEZEZAFZUAUAUDGUAAHIZBCZUEUBEZUEFZG
    AHAUEFZUAUFUDUHAUEBJUIUCUGAUEAUEUBKUILMNUEBOAHBPQRST $.
    $( [28-Oct-1999] $)

  $( The orthogonal projection.  Lemma 4.4(i) of [Beran] p. 111. $)
  pjo $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` ( _|_ ` H ) ) ` A ) =
             ( ( ( proj ` ~H ) ` A ) -h ( ( proj ` H ) ` A ) ) ) $=
    ( cch wcel chil wa cpj cfv cmv co cort wceq cva pjch1 adantl axpjpj eqtr2d
    wb helch pjcli pjhcl choccl sylan hvsubadd syl111anc mpbird eqcomd ) BCDZAE
    DZFZAEGHHZABGHHZIJZABKHZGHHZUJUMUOLZULUOMJZUKLZUJUKAUQUIUKALUHANOABPQUJUKED
    ZULEDUOEDZUPURRUIUSUHAESTOABUAUHUNCDUIUTBUBAUNUAUCUKULUOUDUEUFUG $.
    $( [30-Oct-1999] $)

  ${
    pjidm.1 $e |- H e. CH $.
    pjidm.2 $e |- A e. ~H $.
    $( A projection is idempotent.  Property (ii) of [Beran] p. 109. $)
    pjidmi $p |- ( ( proj ` H ) ` ( ( proj ` H ) ` A ) ) =
               ( ( proj ` H ) ` A ) $=
      ( cpj cfv wcel wceq pjclii pjhclii pjchi mpbi ) ABEFZFZBGNMFNHABCDINBCABC
      DJKL $.
      $( [28-Oct-1999] $)

    ${
      pjadj.3 $e |- B e. ~H $.
      $( A projection is self-adjoint.  Property (i) of [Beran] p. 109. $)
      pjadjii $p |- ( ( ( proj ` H ) ` A ) .ih B ) =
                  ( A .ih ( ( proj ` H ) ` B ) ) $=
        ( cpj cfv cort cva co csp caddc ccj cc0 cch wcel wceq pjorthi ax-mp
        fveq2i cj0 eqtri choccli pjhclii his1i 3eqtr4ri opreq2i chil his7
        mp3an ax-his2 3eqtr4i pjpji opreq1i ) ACGHZHZBUPHZBCIHZGHZHZJKZLKZUQAUT
        HZJKZURLKZUQBLKAURLKUQURLKZUQVALKZMKZVGVDURLKZMKZVCVFVHVJVGMURVDLKZNHZO
        VJVHVMONHOVLONCPQZVLORDBACFESTUAUBUCVDURAUSCDUDZEUEZBCDFUEZUFVNVHORDABC
        EFSTUGUHUQUIQZURUIQZVAUIQVCVIRACDEUEZVQBUSVOFUEUQURVAUJUKVRVDUIQVSVFVKR
        VTVPVQUQVDURULUKUMBVBUQLBCDFUNUHAVEURLACDEUNUOUM $.
        $( [30-Oct-1999] $)

      $( Component of a projection. $)
      pjcompi $p |- ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
                   ( ( proj ` H ) ` ( A +h B ) ) = A ) $=
        ( wcel cort cfv wa cva co cpj wceq hvaddcli pjclii choccli pjpji eqid
        chocunii mp2ani mpanl12 simpld ) ACGBCHIZGJZABKLZCMIIZANZUFUDMIIZBNZUGC
        GZUIUDGZUEUHUJJZUFCDABEFOZPUFUDCDQUNPUKULJUEJUFUGUIKLNUFUFNUMUFCDUNRUFS
        UGUIABUFCDTUAUBUC $.
        $( [31-Oct-1999] $)

      $( Projection of vector sum is sum of projections. $)
      pjaddii $p |- ( ( proj ` H ) ` ( A +h B ) ) =
                  ( ( ( proj ` H ) ` A ) +h ( ( proj ` H ) ` B ) ) $=
        ( cva co cpj cfv cort pjpji opreq12i pjhclii choccli hvadd4i eqtri
        fveq2i wcel wceq csh chshii pjclii shaddcl mp3an hvaddcli pjcompi
        mp2an ) ABGHZCIJZJAUJJZBUJJZGHZACKJZIJZJZBUOJZGHZGHZUJJZUMUIUSUJUIUKUPG
        HZULUQGHZGHUSAVABVBGACDELBCDFLMUKUPULUQACDENZAUNCDOZENZBCDFNZBUNVDFNZPQ
        RUMCSZURUNSZUTUMTCUASUKCSULCSVHCDUBACDEUCBCDFUCUKULCUDUEUNUASUPUNSUQUNS
        VIUNVDUBAUNVDEUCBUNVDFUCUPUQUNUDUEUMURCDUKULVCVFUFUPUQVEVGUFUGUHQ $.
        $( [31-Oct-1999] $)
    $}

    $( The inner product of a projection and its argument is the square of the
       norm of the projection.  Remark in [Halmos] p. 44. $)
    pjinormii $p |- ( ( ( proj ` H ) ` A ) .ih A ) =
                 ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) $=
      ( cpj cfv cno c2 cexp co csp pjhclii normsqi pjadjii pjidmi opreq1i
      3eqtr2ri ) ABEFZFZGFHIJSSKJSRFZAKJSAKJSABCDLZMSABCUADNTSAKABCDOPQ $.
      $( [13-Aug-2000] $)

    ${
      pjmul.3 $e |- C e. CC $.
      $( Projection of (scalar) product is product of projection. $)
      pjmulii $p |- ( ( proj ` H ) ` ( C .h A ) ) =
                  ( C .h ( ( proj ` H ) ` A ) ) $=
        ( csm co cpj cfv cort cva pjpji opreq2i pjhclii choccli hvdistr1i
        eqtri fveq2i wcel wceq csh cc chshii pjclii shmulcl mp3an hvmulcli
        pjcompi mp2an ) BAGHZCIJZJBAULJZGHZBACKJZIJJZGHZLHZULJZUNUKURULUKBUMUPL
        HZGHURAUTBGACDEMNBUMUPFACDEOZAUOCDPZEOZQRSUNCTZUQUOTZUSUNUACUBTBUCTZUMC
        TVDCDUDFACDEUEBUMCUFUGUOUBTVFUPUOTVEUOVBUDFAUOVBEUEBUPUOUFUGUNUQCDBUMFV
        AUHBUPFVCUHUIUJR $.
        $( [31-Oct-1999] $)
    $}

    ${
      pjsub.3 $e |- B e. ~H $.
      $( Projection of vector difference is difference of projections. $)
      pjsubii $p |- ( ( proj ` H ) ` ( A -h B ) ) =
                  ( ( ( proj ` H ) ` A ) -h ( ( proj ` H ) ` B ) ) $=
        ( c1 cneg csm co cva cpj cfv cmv ax-1cn negcli hvmulcli pjaddii
        pjmulii opreq2i eqtri hvsubvali fveq2i pjhclii 3eqtr4i ) AGHZBIJZKJZCLM
        ZMZAUIMZUFBUIMZIJZKJZABNJZUIMUKULNJUJUKUGUIMZKJUNAUGCDEUFBGOPZFQRUPUMUK
        KBUFCDFUQSTUAUOUHUIABEFUBUCUKULACDEUDBCDFUDUBUE $.
        $( [31-Oct-1999] $)
    $}

    ${
      pjsslem.1 $e |- G e. CH $.
      $( Lemma for subset relationships of projections. $)
      pjsslem $p |- ( ( ( proj ` ( _|_ ` H ) ) ` A ) -h
                      ( ( proj ` ( _|_ ` G ) ) ` A ) ) =
                    ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) $=
        ( cort cfv cpj cmv co c0v c1 cneg csm chil cch wcel wceq pjo mp2an
        opreq12i helch pjclii pjhclii hvsubsub4i hvsubid ax-mp opreq1i 3eqtri
        hvsubcli hv2negi hvnegdii ) ACGHIHHZABGHIHHZJKZLACIHHZABIHHZJKZJKZMNUSO
        KURUQJKUPAPIHHZUQJKZVAURJKZJKVAVAJKZUSJKUTUNVBUOVCJCQRAPRZUNVBSDEACTUAB
        QRVEUOVCSFEABTUAUBVAUQVAURAPUCEUDZACDEUEZVFABFEUEZUFVDLUSJVAPRVDLSVFVAU
        GUHUIUJUSUQURVGVHUKULUQURVGVHUMUJ $.
        $( [31-Oct-1999] $)

      $( Subset relationship for projections.  Theorem 4.5(i)->(ii) of [Beran]
         p. 112. $)
      pjss2i $p |- ( H C_ G -> ( ( proj ` H ) ` ( ( proj ` G ) ` A ) ) =
                 ( ( proj ` H ) ` A ) ) $=
        ( wss cort cfv cpj cmv co wcel wceq choccli pjclii chsscon3i ssel mpi
        sylbi csh chshii shsubcl mp3an1 sylancr c0v pjsslem eleq1i pjhclii
        hvsubcli pjoc2i bitri pjsubii eqeq1i hvsubeq0i 3bitri pjidmi eqeq2i
        bitr2i sylibr ) CBGZACHIZJIIZABHIZJIIZKLZVBMZABJIIZCJIZIZAVIIZNZVAVCVBM
        ZVEVBMZVGAVBCDOZEPVAVDVBGZVNCBDFQVPVEVDMVNAVDBFOEPVDVBVERSTVBUAMVMVNVGV
        BVOUBVCVEVBUCUDUEVGVJVKVIIZNZVLVGVHVKKLZVIIZUFNZVJVQKLZUFNVRVGVSVBMWAVF
        VSVBABCDEFUGUHVSCDVHVKABFEUIZACDEUIZUJUKULVTWBUFVHVKCDWCWDUMUNVJVQVHCDW
        CUIVKCDWDUIUOUPVQVKVJACDEUQURUSUT $.
        $( [31-Oct-1999] $)

      $( Projection meet property.  Remark in [Kalmbach] p. 66.  Also Theorem
         4.5(i)->(iv) of [Beran] p. 112. $)
      pjssmii $p |- ( H C_ G -> ( ( ( proj ` G ) ` A )
      -h ( ( proj ` H ) ` A ) ) = ( ( proj ` ( G i^i ( _|_ ` H ) ) ) ` A ) ) $=
        ( wss cort cfv cin cpj cmv co c1 cneg csm cva choccli chincli pjhclii
        pjsubii hvsubvali c0v wceq inss1 pjss2i ax-mp wcel csh chshii shococss
        inss2 chsscon3i mpbi sstri pjclii sselii pjoc2i opreq2i cc ax-1cn
        negcli hvmul0 eqtri opreq12i chil ax-hvaddid 3eqtri wa ssel mpi
        shsubcl mp3an1 sylancr pjsslem sylbi syl5eqelr jca elin hvsubcli pjchi
        bitr3i sylib syl5reqr ) CBGZABCHIZJZKIZIZABKIIZACKIIZLMZWHIZWLWMWJWHIZW
        KWHIZLMWNNOZWOPMZQMZWIWJWKWGBWFFCDRZSZABFETZACDETZUAWNWOWJWGWTXATWKWGWT
        XBTUBWRWIUCQMZWIWNWIWQUCQWGBGWNWIUDBWFUEABWGWTEFUFUGWQWPUCPMZUCWOUCWPPW
        KWGHIZUHWOUCUDCXEWKCWFHIZXECUIUHCXFGCDUJCUKUGWGWFGXFXEGBWFULWGWFWTWSUMU
        NUOACDEUPZUQWKWGWTXBURUNUSWPUTUHXDUCUDNVAVBWPVCUGVDVEWIVFUHXCWIUDAWGWTE
        TWIVGUGVDVHWEWLBUHZWLWFUHZVIZWMWLUDZWEXHXIWEWJBUHZWKBUHZXHABFEUPWEWKCUH
        XMXGCBWKVJVKBUIUHXLXMXHBFUJWJWKBVLVMVNWEWLAWFKIIZABHIZKIIZLMZWFABCDEFVO
        WEXOWFGZXQWFUHZCBDFUMXRXNWFUHZXPWFUHZXSAWFWSEUPXRXPXOUHYAAXOBFREUPXOWFX
        PVJVKWFUIUHXTYAXSWFWSUJXNXPWFVLVMVNVPVQVRXJWLWGUHXKWLBWFVSWLWGWTWJWKXAX
        BVTWAWBWCWD $.
        $( [31-Oct-1999] $)

      $( Theorem 4.5(iv)->(v) of [Beran] p. 112. $)
      pjssge0ii $p |- ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) )
                    = ( ( proj ` ( G i^i ( _|_ ` H ) ) ) ` A ) ->
   0 <_ ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) .ih A ) ) $=
        ( cpj cfv cmv co cort cin wceq cc0 cno c2 cexp csp cle choccli chincli
        pjhclii normcli sqge0i opreq1 pjinormii syl6eq syl5breqr ) ABGHHACGHHIJ
        ZABCKHZLZGHHZMZNULOHZPQJZUIARJZSUNULAUKBUJFCDTUAZEUBUCUDUMUPULARJUOUIUL
        ARUEAUKUQEUFUGUH $.
        $( [13-Aug-2000] $)

      $( Theorem 4.5(v)<->(vi) of [Beran] p. 112. $)
      pjdifnormii $p |- ( 0 <_
                 ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) .ih A ) <->
      ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` ( ( proj ` G ) ` A ) ) ) $=
        ( cc0 cpj cfv cno c2 cexp co cmin cle wbr cmv csp pjhclii normcli
        resqcli subge0i chil wcel wceq his2sub mp3an pjinormii opreq12i eqtri
        breq2i wb normge0 ax-mp le2sqi mp2an 3bitr4i ) GABHIIZJIZKLMZACHIIZJIZK
        LMZNMZOPVCUTOPZGURVAQMARMZOPVBUSOPZUTVCUSURABFESZTZUAVBVAACDESZTZUAUBVF
        VDGOVFURARMZVAARMZNMZVDURUCUDZVAUCUDZAUCUDVFVNUEVHVJEURVAAUFUGVLUTVMVCN
        ABFEUHACDEUHUIUJUKGVBOPZGUSOPZVGVEULVPVQVJVAUMUNVOVRVHURUMUNVBUSVKVIUOU
        PUQ $.
        $( [13-Aug-2000] $)

      $( The projection on a subspace join is the sum of the projections. $)
      pjcji $p |- ( H C_ ( _|_ ` G ) -> ( ( proj ` ( H vH G ) ) ` A ) =
                ( ( ( proj ` H ) ` A ) +h ( ( proj ` G ) ` A ) ) ) $=
        ( cort cfv wss cpj cva co chj cmv cin choccli pjssmii opreq2d c1 cneg
        csm chil wcel wceq pjhclii hvaddsub12 mp3an pjpoi opreq2i hvnegdii
        3eqtr4i hvsubcli hvsubvali eqtr4i chjcomi chdmm4i fveq2i fveq1i
        chincli pjopi eqtri 3eqtr4g eqcomd ) CBGHZIZACJHHZABJHHZKLZACBMLZJHZHZV
        EAAVDJHHZVFNLZNLZAAVDCGHZOZJHHZNLZVHVKVEVMVQANAVDCDEBFPZQRVHASTVMUALZKL
        ZVNVFAVLNLZKLZAVFVLNLZKLZVHWAVFUBUCAUBUCVLUBUCWCWEUDACDEUEZEAVDVSEUEZVF
        AVLUFUGVGWBVFKABFEUHUIVTWDAKVLVFWGWFUJUIUKAVMEVLVFWGWFULUMUNVKAVPGHZJHZ
        HVRAVJWIVIWHJVIBCMLWHCBDFUOBCFDUPUNUQURAVPVDVOVSCDPUSEUTVAVBVC $.
        $( [1-Nov-1999] $)
    $}
  $}

  ${
    pjadjt.1 $e |- H e. CH $.
    $( A projection is self-adjoint.  Property (i) of [Beran] p. 109. $)
    pjadji $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( ( proj ` H ) ` A ) .ih B ) =
                  ( A .ih ( ( proj ` H ) ` B ) ) ) $=
      ( chil wcel cpj cfv csp co wceq c0v cif fveq2 opreq1d opreq1 eqeq12d
      opreq2 opreq2d ax-hv0cl elimel pjadjii dedth2h ) AEFZBEFZACGHZHZBIJZABUFH
      ZIJZKUDALMZUFHZBIJZUKUIIJZKULUEBLMZIJZUKUOUFHZIJZKABLLAUKKZUHUMUJUNUSUGUL
      BIAUKUFNOAUKUIIPQBUOKZUMUPUNURBUOULIRUTUIUQUKIBUOUFNSQUKUOCDALETUABLETUAU
      BUC $.
      $( [6-Oct-2000] $)

    $( Projection of vector sum is sum of projections. $)
    pjaddi $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( proj ` H ) ` ( A +h B ) ) =
                ( ( ( proj ` H ) ` A ) +h ( ( proj ` H ) ` B ) ) ) $=
      ( chil wcel cva co cpj cfv wceq c0v cif opreq1 fveq2d fveq2 opreq1d
      eqeq12d opreq2 opreq2d ax-hv0cl elimel pjaddii dedth2h ) AEFZBEFZABGHZCIJ
      ZJZAUHJZBUHJZGHZKUEALMZBGHZUHJZUMUHJZUKGHZKUMUFBLMZGHZUHJZUPURUHJZGHZKABL
      LAUMKZUIUOULUQVCUGUNUHAUMBGNOVCUJUPUKGAUMUHPQRBURKZUOUTUQVBVDUNUSUHBURUMG
      SOVDUKVAUPGBURUHPTRUMURCDALEUAUBBLEUAUBUCUD $.
      $( [14-Nov-2000] $)

    $( The inner product of a projection and its argument is the square of the
       norm of the projection.  Remark in [Halmos] p. 44. $)
    pjinormi $p |- ( A e. ~H -> ( ( ( proj ` H ) ` A ) .ih A ) =
                 ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) ) $=
      ( chil wcel cpj cfv csp co cno c2 cexp wceq c0v cif fveq2 id opreq12d
      fveq2d opreq1d eqeq12d ax-hv0cl elimel pjinormii dedth ) ADEZABFGZGZAHIZU
      HJGZKLIZMUFANOZUGGZULHIZUMJGZKLIZMANAULMZUIUNUKUPUQUHUMAULHAULUGPZUQQRUQU
      JUOKLUQUHUMJURSTUAULBCANDUBUCUDUE $.
      $( [2-Jun-2006] $)

    $( Projection of vector difference is difference of projections. $)
    pjsubi $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( proj ` H ) ` ( A -h B ) ) =
                ( ( ( proj ` H ) ` A ) -h ( ( proj ` H ) ` B ) ) ) $=
      ( chil wcel cmv co cpj cfv wceq c0v cif opreq1 fveq2d fveq2 opreq1d
      eqeq12d opreq2 opreq2d ax-hv0cl elimel pjsubii dedth2h ) AEFZBEFZABGHZCIJ
      ZJZAUHJZBUHJZGHZKUEALMZBGHZUHJZUMUHJZUKGHZKUMUFBLMZGHZUHJZUPURUHJZGHZKABL
      LAUMKZUIUOULUQVCUGUNUHAUMBGNOVCUJUPUKGAUMUHPQRBURKZUOUTUQVBVDUNUSUHBURUMG
      SOVDUKVAUPGBURUHPTRUMURCDALEUAUBBLEUAUBUCUD $.
      $( [14-Nov-2000] $)

    $( Projection of scalar product is scalar product of projection. $)
    pjmuli $p |- ( ( A e. CC /\ B e. ~H ) -> ( ( proj ` H ) ` ( A .h B ) ) =
                ( A .h ( ( proj ` H ) ` B ) ) ) $=
      ( cc wcel chil csm co cpj cfv wceq cc0 cif c0v opreq1 fveq2d eqeq12d
      opreq2 fveq2 opreq2d ax-hv0cl elimel 0cn pjmulii dedth2h ) AEFZBGFZABHIZC
      JKZKZABUJKZHIZLUGAMNZBHIZUJKZUNULHIZLUNUHBONZHIZUJKZUNURUJKZHIZLABMOAUNLZ
      UKUPUMUQVCUIUOUJAUNBHPQAUNULHPRBURLZUPUTUQVBVDUOUSUJBURUNHSQVDULVAUNHBURU
      JTUARURUNCDBOGUBUCAMEUDUCUEUF $.
      $( [26-Nov-2000] $)

    $( The inner product of a projection and its argument is nonnegative. $)
    pjige0i $p |- ( A e. ~H -> 0 <_ ( ( ( proj ` H ) ` A ) .ih A ) ) $=
      ( chil wcel cc0 cpj cfv cno c2 cexp co csp cle cr wbr pjhcli normcl
      sqge0 3syl pjinormi breqtrrd ) ADEZFABGHHZIHZJKLZUDAMLNUCUDDEUEOEFUFNPABC
      QUDRUESTABCUAUB $.
      $( [2-Jun-2006] $)
  $}

  $( The inner product of a projection and its argument is nonnegative. $)
  pjige0 $p |- ( ( H e. CH /\ A e. ~H ) ->
                0 <_ ( ( ( proj ` H ) ` A ) .ih A ) ) $=
    ( cch wcel chil cc0 cpj cfv csp co cle wbr wi c0h cif wceq fveq2 fveq1d
    opreq1d breq2d imbi2d h0elch elimel pjige0i dedth imp ) BCDZAEDZFABGHZHZAIJ
    ZKLZUGUHULMUHFAUGBNOZGHZHZAIJZKLZMBNBUMPZULUQUHURUKUPFKURUJUOAIURAUIUNBUMGQ
    RSTUAAUMBNCUBUCUDUEUF $.
    $( [2-Jun-2006] $)

  $( The projection on a subspace join is the sum of the projections. $)
  pjcjt2 $p |- ( ( H e. CH /\ G e. CH /\ A e. ~H ) ->
            ( H C_ ( _|_ ` G ) -> ( ( proj ` ( H vH G ) ) ` A ) =
            ( ( ( proj ` H ) ` A ) +h ( ( proj ` G ) ` A ) ) ) ) $=
    ( cch wcel chil cort cfv wss chj co cpj cva wceq wi cif c0v sseq1 opreq1
    fveq2d fveq1d fveq2 opreq1d eqeq12d imbi12d sseq2d opreq2 opreq2d opreq12d
    imbi2d helch elimel ax-hv0cl pjcji dedth3h ) CDEZBDEZAFEZCBGHZIZACBJKZLHZHZ
    ACLHZHZABLHZHZMKZNZOUPCFPZUSIZAVJBJKZLHZHZAVJLHZHZVGMKZNZOVJUQBFPZGHZIZAVJV
    SJKZLHZHZVPAVSLHZHZMKZNZOWAURAQPZWCHZWIVOHZWIWEHZMKZNZOCBAFFQCVJNZUTVKVIVRC
    VJUSRWOVCVNVHVQWOAVBVMWOVAVLLCVJBJSTUAWOVEVPVGMWOAVDVOCVJLUBUAUCUDUEBVSNZVK
    WAVRWHWPUSVTVJBVSGUBUFWPVNWDVQWGWPAVMWCWPVLWBLBVSVJJUGTUAWPVGWFVPMWPAVFWEBV
    SLUBUAUHUDUEAWINZWHWNWAWQWDWJWGWMAWIWCUBWQVPWKWFWLMAWIVOUBAWIWEUBUIUDUJWIVS
    VJCFDUKULAQFUMULBFDUKULUNUO $.
    $( [1-Nov-1999] $)

  ${
    pj0.1 $e |- H e. CH $.
    $( The projection of the zero vector. $)
    pj0i $p |- ( ( proj ` H ) ` 0h ) = 0h $=
      ( c0v cort cfv wcel cpj wceq csh chshii oc0 ax-mp ax-hv0cl pjoc2i mpbi )
      CADEFZCAGEECHAIFPABJAKLCABMNO $.
      $( [31-Oct-1999] $)
  $}

  $( Projection of a vector in the projection subspace.  Lemma 4.4(ii) of
     [Beran] p. 111. $)
  pjch $p |- ( ( H e. CH /\ A e. ~H ) ->
              ( A e. H <-> ( ( proj ` H ) ` A ) = A ) ) $=
    ( cch wcel chil cpj cfv wceq wb cif c0v eleq2 fveq2 fveq1d eqeq1d bibi12d
    eleq1 id eqeq12d helch elimel ax-hv0cl pjchi dedth2h ) BCDZAEDZABDZABFGZGZA
    HZIAUEBEJZDZAUKFGZGZAHZIUFAKJZUKDZUPUMGZUPHZIBAEKBUKHZUGULUJUOBUKALUTUIUNAU
    TAUHUMBUKFMNOPAUPHZULUQUOUSAUPUKQVAUNURAUPAUPUMMVARSPUPUKBECTUAAKEUBUAUCUD
    $.
    $( [30-Oct-1999] $)

  $( The projection of a vector in the projection subspace is itself. $)
  pjid $p |- ( ( H e. CH /\ A e. H ) -> ( ( proj ` H ) ` A ) = A ) $=
    ( cch wcel chil wa cpj cfv wceq simpl chel jca pjch biimpa sylancom ) BCDZA
    BDZPAEDZFZABGHHAIZPQFPRPQJABKLSQTABMNO $.
    $( [9-Apr-2006] $)

  ${
    $d x H $.
    $( The set of vectors belonging to the subspace of a projection.  Part of
       Theorem 26.2 of [Halmos] p. 44. $)
    pjvec $p |- ( H e. CH -> H = { x e. ~H | ( ( proj ` H ) ` x ) = x } ) $=
      ( cch wcel chil cin cv cpj cfv wceq crab wss chss sseqin2 sylib wb pjch
      ex rabbirdv eqtr3d ) BCDZEBFZBAGZBHIIUCJZAEKUABELUBBJBMBENOUAUDABEUAUCEDU
      CBDUDPUCBQRST $.
      $( [11-Apr-2006] $)

    $( The set of vectors belonging to the orthocomplemented subspace of a
       projection.  Second part of Theorem 27.3 of [Halmos] p. 45. $)
    pjocvec $p |- ( H e. CH -> ( _|_ ` H ) =
                   { x e. ~H | ( ( proj ` H ) ` x ) = 0h } ) $=
      ( cch wcel chil cort cfv cin cv cpj c0v wceq crab wss choccl chss syl
      sseqin2 sylib wb pjoc2 ex rabbirdv eqtr3d ) BCDZEBFGZHZUFAIZBJGGKLZAEMUEU
      FENZUGUFLUEUFCDUJBOUFPQUFERSUEUIAUFEUEUHEDUHUFDUITUHBUAUBUCUD $.
      $( [24-Apr-2006] $)
  $}

  ${
    pjocin.1 $e |- G e. CH $.
    pjocin.2 $e |- H e. CH $.
    $( Membership of projection in orthocomplement of intersection. $)
    pjocini $p |- ( A e. ( _|_ ` ( G i^i H ) ) ->
                 ( ( proj ` G ) ` A ) e. ( _|_ ` ( G i^i H ) ) ) $=
      ( cin cort cfv wcel cpj cmv co cch chil wceq chincli choccli cheli pjpo
      sylancr pjcli wss inss1 chsscon3i mpbi sseli 3syl csh chshii shsubcl
      mp3an1 mpdan eqeltrd ) ABCFZGHZIZABJHHZAABGHZJHHZKLZUOUPBMIANIZUQUTODAUOU
      NBCDEPZQZRZABSTUPUSUOIZUTUOIZUPVAUSURIVEVDAURBDQUAURUOUSUNBUBURUOUBBCUCUN
      BVBDUDUEUFUGUOUHIUPVEVFUOVCUIAUSUOUJUKULUM $.
      $( [21-Apr-2001] $)

    $( Membership of projection in an intersection. $)
    pjini $p |- ( A e. ( G i^i H ) ->
                 ( ( proj ` G ) ` A ) e. ( G i^i H ) ) $=
      ( cin wcel cpj cfv wceq inss1 sseli cch pjid mpan syl eleq1d ibir ) ABCFZ
      GZABHIIZSGTUAASTABGZUAAJZSBABCKLBMGUBUCDABNOPQR $.
      $( [22-Apr-2001] $)
  $}

  ${
    $d x y z w G $.  $d x y z w H $.
    pjjs.1 $e |- G e. CH $.
    pjjs.2 $e |- H e. SH $.
    $( A sufficient condition for subspace join to be equal to subspace sum. $)
    pjjsi $p |- ( A. x e. ( G vH H ) ( ( proj ` ( _|_ ` G ) ) ` x ) e. H ->
               ( G vH H ) = ( G +H H ) ) $=
      ( vw vy vz cv cort cfv cpj wcel chj co wral cph wss wa wceq fveq2 eleq1d
      rcla4v cva wrex chil chshii shjcli cheli pjcli anim1i cch axpjpj mpan
      adantr jca sylan rcla4eopr 3expa syl shseli sylibr ex syld com12 ssrdv
      shsleji jctir eqss ) AIZBJKLKZKZCMZABCNOZPZVNBCQOZRZVPVNRZSVNVPTVOVQVRVOF
      VNVPFIZVNMZVOVSVPMZVTVOVSVKKZCMZWAVMWCAVSVNVJVSTVLWBCVJVSVKUAUBUCVTWCWAVT
      WCSZVSGIHIUDOTHCUEGBUEZWAWDVSBLKKZBMZWCSZVSWFWBUDOTZSZWEVTVSUFMZWCWJVSVNB
      CBDUGZEUHUIWKWCSWHWIWKWGWCVSBDUJUKWKWIWCBULMWKWIDVSBUMUNUOUPUQWGWCWIWEGHB
      CWFWBVSUDURUSUTGHBCVSWLEVAVBVCVDVEVFBCWLEVGVHVNVPVIVB $.
      $( [29-May-2004] $)
  $}

  ${
    $d x y z w H $.
    pjfn.1 $e |- H e. CH $.
    $( Functionality of a projection. $)
    pjfni $p |- ( proj ` H ) Fn ~H $=
      ( vx vy vz vw chil cv cva co wceq cort cfv wrex crab cuni cpj cch
      elisseti rabex uniex wcel wa copab pjmval ax-mp fnopab2 ) CDGCHZEHFHIJKFA
      LMNZEAOZPZAQMZUJUIEAARBSTUAARUBULUHGUBDHUKKUCCDUDKBCDEFAUEUFUG $.
      $( [30-Oct-1999] $)

    $( The range of a projection.  Part of Theorem 26.2 of [Halmos] p. 44. $)
    pjrni $p |- ran ( proj ` H ) = H $=
      ( vx vy cv chil wcel cpj cfv wceq wa copab crn wex cab rnopab wfn pjfni
      dffn5 mpbi rneqi cheli cch pjid mpan eqcomd visset eleq1 fveq2 eqeq2d
      anbi12d cla4ev syl11anc pjcli syl5ibr impcom 19.23aiv impbii abbi2i
      3eqtr4i ) CEZFGZDEZVAAHIZIZJZKZCDLZMVGCNZDOVDMAVGCDPVDVHVDFQVDVHJABRCDFVD
      STUAVIDAVCAGZVIVJVCFGZVCVCVDIZJZVIVCABUBVJVLVCAUCGVJVLVCJBVCAUDUEUFVGVKVM
      KCVCDUGVAVCJZVBVKVFVMVAVCFUHVNVEVLVCVAVCVDUIUJUKULUMVGVJCVFVBVJVBVJVFVEAG
      VAABUNVCVEAUHUOUPUQURUSUT $.
      $( [30-Oct-1999] $)

    $( A projection maps onto its subspace. $)
    pjfoi $p |- ( proj ` H ) : ~H -onto-> H $=
      ( chil cpj cfv wfo wfn crn wceq df-fo pjfni pjrni mpbir2an ) CAADEZFNCGNH
      AICANJABKABLM $.
      $( [24-Apr-2006] $)

    $( The mapping of a projection. $)
    pjfi $p |- ( proj ` H ) : ~H --> ~H $=
      ( chil cpj cfv wf wfn crn wss df-f pjfni pjrni chssii eqsstri mpbir2an )
      CCADEZFPCGPHZCICCPJABKQACABLABMNO $.
      $( [11-Nov-2000] $)

    $( The value of a projection in terms of components. $)
    pjvi $p |- ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
               ( ( proj ` H ) ` ( A +h B ) ) = A ) $=
      ( wcel cort cfv wa cva co cpj c0v chil wceq cheli choccli pjaddi syl2an
      cch pjid mpan wb pjoc2 sylancr ibi opreqan12d ax-hvaddid syl adantr
      3eqtrd ) ACEZBCFGZEZHABIJCKGZGZAUNGZBUNGZIJZALIJZAUKAMEZBMEZUOURNUMACDOZB
      ULCDPOZABCDQRUKUMUPAUQLICSEZUKUPANDACTUAUMUQLNZUMVDVAUMVEUBDVCBCUCUDUEUFU
      KUSANZUMUKUTVFVBAUGUHUIUJ $.
      $( [28-Nov-2000] $)
  $}

  $( A projection maps onto its subspace. $)
  pjfo $p |- ( H e. CH -> ( proj ` H ) : ~H -onto-> H ) $=
    ( cch wcel chil cpj cfv wfo c0h cif wceq wb fveq2 foeq1 syl foeq3 h0elch
    elimel pjfoi dedth2v ) ABCZDAAEFZGZDATAHIZEFZGZDUCUDGAAHHAUCJUAUDJUBUEKAUCE
    LDAUAUDMNAUCDUDOUCAHBPQRS $.
    $( [24-Apr-2006] $)

  $( The range of a projection.  Part of Theorem 26.2 of [Halmos] p. 44. $)
  pjrn $p |- ( H e. CH -> ran ( proj ` H ) = H ) $=
    ( cch wcel chil cpj cfv wfo crn wceq pjfo forn syl ) ABCDAAEFZGMHAIAJDAMKL
    $.
    $( [24-Apr-2006] $)

  $( The mapping of a projection. $)
  pjf $p |- ( H e. CH -> ( proj ` H ) : ~H --> ~H ) $=
    ( cch wcel chil cpj cfv wf wss wfo pjfo fof syl chss fss syl11anc ) ABCZDAA
    EFZGZADHDDQGPDAQIRAJDAQKLAMDADQNO $.
    $( [24-Apr-2006] $)

  $( Functionality of a projection. $)
  pjfn $p |- ( H e. CH -> ( proj ` H ) Fn ~H ) $=
    ( cch wcel chil cpj cfv wf wfn pjf ffn syl ) ABCDDAEFZGLDHAIDDLJK $.
    $( [30-May-2006] $)

  ${
    pjsumt.1 $e |- G e. CH $.
    pjsumt.2 $e |- H e. CH $.
    $( The projection on a subspace sum is the sum of the projections. $)
    pjsumi $p |- ( A e. ~H ->
            ( G C_ ( _|_ ` H ) -> ( ( proj ` ( G +H H ) ) ` A ) =
            ( ( ( proj ` G ) ` A ) +h ( ( proj ` H ) ` A ) ) ) ) $=
      ( chil wcel cort cfv wss cph co cpj cva wceq wa chj osumi fveq2d fveq1d
      adantl cch wi pjcjt2 mp3an12 imp eqtrd ex ) AFGZBCHIJZABCKLZMIZIZABMIIACM
      IINLZOUIUJPUMABCQLZMIZIZUNUJUMUQOUIUJAULUPUJUKUOMBCDERSTUAUIUJUQUNOZBUBGC
      UBGUIUJURUCDEACBUDUEUFUGUH $.
      $( [11-Nov-2000] $)

    $( One-to-one correspondence of projection and subspace. $)
    pj11i $p |- ( ( proj ` G ) = ( proj ` H ) <-> G = H ) $=
      ( cpj cfv wceq crn rneq pjrni 3eqtr3g fveq2 impbii ) AEFZBEFZGZABGPNHOHAB
      NOIACJBDJKABELM $.
      $( [26-Nov-2000] $)

    $( Vector decomposition into sum of projections on orthogonal subspaces. $)
    pjdsi $p |- ( ( A e. ( G vH H ) /\ G C_ ( _|_ ` H ) ) ->
               A = ( ( ( proj ` G ) ` A ) +h ( ( proj ` H ) ` A ) ) ) $=
      ( chj co wcel cort cfv wss wa cph cpj cva osumi fveq2d fveq1d cch wceq
      chjcli pjid mpan sylan9eqr chil cheli pjsumi imp sylan eqtr3d ) ABCFGZHZB
      CIJKZLABCMGZNJZJZAABNJJACNJJOGZUMULUPAUKNJZJZAUMAUOURUMUNUKNBCDEPQRUKSHUL
      USATBCDEUAZAUKUBUCUDULAUEHZUMUPUQTZAUKUTUFVAUMVBABCDEUGUHUIUJ $.
      $( [21-Jun-2006] $)
  $}

  ${
    pjds3.1 $e |- F e. CH $.
    pjds3.2 $e |- G e. CH $.
    pjds3.3 $e |- H e. CH $.
    $( Vector decomposition into sum of projections on orthogonal subspaces. $)
    pjds3i $p |- ( ( ( A e. ( ( F vH G ) vH H ) /\ F C_ ( _|_ ` G ) ) /\
     ( F C_ ( _|_ ` H ) /\ G C_ ( _|_ ` H ) ) ) -> A = ( ( ( ( proj ` F ) ` A )
                +h ( ( proj ` G ) ` A ) ) +h ( ( proj ` H ) ` A ) ) ) $=
      ( chj co wcel cort cfv wss wa cpj cva cph wceq simpl choccli chlubii
      chjcli pjdsi syl2an osumi fveq2d fveq1d opreq1d ad2antlr chil cheli
      pjsumi imp sylan adantr 3eqtr2d ) ABCHIZDHIZJZBCKLMZNZBDKLZMCVBMNZNAAUQOL
      ZLZADOLLZPIZABCQIZOLZLZVFPIZABOLLACOLLPIZVFPIZVAUSUQVBMAVGRVCUSUTSBCVBEFD
      GTUAAUQDBCEFUBZGUCUDUTVKVGRUSVCUTVJVEVFPUTAVIVDUTVHUQOBCEFUEUFUGUHUIVAVKV
      MRVCVAVJVLVFPUSAUJJZUTVJVLRZAURUQDVNGUBUKVOUTVPABCEFULUMUNUHUOUP $.
      $( [22-Jun-2006] $)
  $}

  $( One-to-one correspondence of projection and subspace. $)
  pj11 $p |- ( ( G e. CH /\ H e. CH ) ->
              ( ( proj ` G ) = ( proj ` H ) <-> G = H ) ) $=
    ( cch wcel cpj cfv wceq wb c0h cif fveq2 eqeq1d eqeq1 bibi12d eqeq2d eqeq2
    h0elch elimel pj11i dedth2h ) ACDZBCDZAEFZBEFZGZABGZHUAAIJZEFZUDGZUGBGZHUHU
    BBIJZEFZGZUGUKGZHABIIAUGGZUEUIUFUJUOUCUHUDAUGEKLAUGBMNBUKGZUIUMUJUNUPUDULUH
    BUKEKOBUKUGPNUGUKAICQRBICQRST $.
    $( [24-Apr-2006] $)

  ${
    $d f h w x y z $.
    $( Functionality of the projection function. $)
    pjmfn $p |- proj Fn CH $=
      ( vh vf vx vy vz vw cch cv chil wcel cva co wceq cort cfv wrex crab cuni
      wa copab cpj ax-hilex opabex2 df-pj fnopab2 ) ABGCHZIJDHUFEHFHKLMFAHZNOPE
      UGQRZMSCDTUACDIUHUBUCCDEFBAUDUE $.
      $( [24-Apr-2006] $)

    $( The projector function maps one-to-one into the set of Hilbert space
       operators. $)
    pjmf1 $p |- proj : CH -1-1-> ( ~H ^m ~H ) $=
      ( vx vy cch chil cmap co cpj wf1 wf cv cfv wceq wi wral dff13 wfn wcel
      ffnfv pjmfn pjf ax-hilex elmap sylibr rgen mpbir2an wa pj11 biimpd
      rgen2a ) CDDEFZGHCUJGIZAJZGKZBJZGKLZULUNLZMZBCNACNABCUJGOUKGCPUMUJQZACNAC
      UJGRSURACULCQZDDUMIURULTDDUMUAUAUBUCUDUEUQABCUSUNCQUFUOUPULUNUGUHUIUE $.
      $( [24-Apr-2006] $)
  $}

  $( The inner product of projections on orthogonal subspaces vanishes. $)
  pjoi0 $p |- ( ( ( G e. CH /\ H e. CH /\ A e. ~H ) /\ G C_ ( _|_ ` H ) ) ->
               ( ( ( proj ` G ) ` A ) .ih ( ( proj ` H ) ` A ) ) = 0 ) $=
    ( cch wcel chil w3a cort cfv wss cpj crn csp co cc0 wceq wa pjrn adantr
    fveq2d adantl sseq12d biimpar 3adantl3 csh id eqeltrd chsh syl 3ad2ant2
    simpr wfn pjfn fnfvelrn sylan 3adant2 3adant1 jca shorth syl3c syldan ) BDE
    ZCDEZAFEZGZBCHIZJZBKIZLZCKIZLZHIZJZAVHIZAVJIZMNOPZVBVCVGVMVDVBVCQZVMVGVQVIB
    VLVFVBVIBPVCBRSVCVLVFPVBVCVKCHCRZTUAUBUCUDVEVMQVKUEEZVMVNVIEZVOVKEZQZVPVEVS
    VMVCVBVSVDVCVKDEVSVCVKCDVRVCUFUGVKUHUIUJSVEVMUKVEWBVMVEVTWAVBVDVTVCVBVHFULV
    DVTBUMFAVHUNUOUPVCVDWAVBVCVJFULVDWACUMFAVJUNUOUQURSVNVOVIVKUSUTVA $.
    $( [30-Jun-2006] $)

  ${
    pjoi0.1 $e |- G e. CH $.
    pjoi0.2 $e |- H e. CH $.
    pjoi0.3 $e |- A e. ~H $.
    $( The inner product of projections on orthogonal subspaces vanishes. $)
    pjoi0i $p |- ( G C_ ( _|_ ` H ) ->
                ( ( ( proj ` G ) ` A ) .ih ( ( proj ` H ) ` A ) ) = 0 ) $=
      ( cch wcel chil w3a cort cfv wss cpj csp co cc0 wceq 3pm3.2i pjoi0 mpan
      ) BGHZCGHZAIHZJBCKLMABNLLACNLLOPQRUBUCUDDEFSABCTUA $.
      $( [1-Nov-1999] $)

    $( Pythagorean theorem for projections on orthogonal subspaces. $)
    pjopythi $p |- ( G C_ ( _|_ ` H ) ->
       ( ( normh ` ( ( ( proj ` G ) ` A ) +h ( ( proj ` H ) ` A ) ) ) ^ 2 ) =
                 ( ( ( normh ` ( ( proj ` G ) ` A ) ) ^ 2 ) +
                   ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) ) ) $=
      ( cort cfv wss cpj csp co cc0 wceq cva cno c2 cexp caddc pjoi0i pjhclii
      normpythi syl ) BCGHIABJHHZACJHHZKLMNUDUEOLPHQRLUDPHQRLUEPHQRLSLNABCDEFTU
      DUEABDFUAACEFUAUBUC $.
      $( [1-Nov-1999] $)
  $}

  $( Pythagorean theorem for projections on orthogonal subspaces. $)
  pjopyth $p |- ( ( H e. CH /\ G e. CH /\ A e. ~H ) -> ( H C_ ( _|_ ` G ) ->
       ( ( normh ` ( ( ( proj ` H ) ` A ) +h ( ( proj ` G ) ` A ) ) ) ^ 2 ) =
                 ( ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) +
                   ( ( normh ` ( ( proj ` G ) ` A ) ) ^ 2 ) ) ) ) $=
    ( cch wcel chil cort cfv wss cpj cva co cno c2 cexp caddc wceq wi cif c0v
    sseq1 fveq2 fveq1d opreq1d fveq2d eqeq12d imbi12d sseq2d opreq2d opreq12d
    imbi2d helch elimel ax-hv0cl pjopythi dedth3h ) CDEZBDEZAFEZCBGHZIZACJHZHZA
    BJHZHZKLZMHZNOLZVCMHZNOLZVEMHZNOLZPLZQZRUQCFSZUTIZAVOJHZHZVEKLZMHZNOLZVRMHZ
    NOLZVLPLZQZRVOURBFSZGHZIZVRAWFJHZHZKLZMHZNOLZWCWJMHZNOLZPLZQZRWHUSATSZVQHZW
    RWIHZKLZMHZNOLZWSMHZNOLZWTMHZNOLZPLZQZRCBAFFTCVOQZVAVPVNWECVOUTUAXJVHWAVMWD
    XJVGVTNOXJVFVSMXJVCVRVEKXJAVBVQCVOJUBUCZUDUEUDXJVJWCVLPXJVIWBNOXJVCVRMXKUEU
    DUDUFUGBWFQZVPWHWEWQXLUTWGVOBWFGUBUHXLWAWMWDWPXLVTWLNOXLVSWKMXLVEWJVRKXLAVD
    WIBWFJUBUCZUIUEUDXLVLWOWCPXLVKWNNOXLVEWJMXMUEUDUIUFUGAWRQZWQXIWHXNWMXCWPXHX
    NWLXBNOXNWKXAMXNVRWSWJWTKAWRVQUBZAWRWIUBZUJUEUDXNWCXEWOXGPXNWBXDNOXNVRWSMXO
    UEUDXNWNXFNOXNWJWTMXPUEUDUJUFUKWRVOWFCFDULUMBFDULUMATFUNUMUOUP $.
    $( [2-Nov-1999] $)

  ${
    pjnorm.1 $e |- H e. CH $.
    pjnorm.2 $e |- A e. ~H $.
    $( The norm of the projection is less than or equal to the norm. $)
    pjnormi $p |- ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` A ) $=
      ( cpj cfv cno cort cva co cle chil wcel wa csp cc0 wceq wbr pjhclii
      choccli pm3.2i cch pjorthi ax-mp normpyc mp2 pjpji fveq2i breqtrri ) ABEF
      FZGFZUJABHFZEFFZIJZGFZAGFKUJLMZUMLMZNUJUMOJPQZUKUOKRUPUQABCDSAULBCTDSUABU
      BMURCAABDDUCUDUJUMUEUFAUNGABCDUGUHUI $.
      $( [27-Oct-1999] $)

    $( Pythagorean theorem for projections. $)
    pjpythi $p |- ( ( normh ` A ) ^ 2 ) =
                 ( ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) +
                   ( ( normh ` ( ( proj ` ( _|_ ` H ) ) ` A ) ) ^ 2 ) ) $=
      ( cno cfv c2 cexp co cpj cort cva caddc pjpji fveq2i opreq1i wss wceq
      csh wcel chshii shococss ax-mp choccli pjopythi eqtri ) AEFZGHIABJFFZABKF
      ZJFFZLIZEFZGHIZUHEFGHIUJEFGHIMIZUGULGHAUKEABCDNOPBUIKFQZUMUNRBSTUOBCUABUB
      UCABUICBCUDDUEUCUF $.
      $( [27-Oct-1999] $)

    $( If a vector does not belong to subspace, the norm of its projection is
       less than its norm. $)
    pjneli $p |- ( -. A e. H <->
                ( normh ` ( ( proj ` H ) ` A ) ) < ( normh ` A ) ) $=
      ( cno cfv cpj wne cle wbr wa wcel wn clt pjnormi biantrur cort c0v wceq
      c2 cexp co pjoc1i caddc cc0 pjhclii normcli resqcli recni choccli 0cn
      sqcli addcani pjpythi sq0 opreq2i addid1i eqtr2i eqeq12i wb chil normge0
      ax-mp 0re leidi sq11i mp2an norm-i.i bitr2i 3bitr4ri 3bitri necon3bbii
      ltleni 3bitr4i ) AEFZABGFFZEFZHZVQVOIJZVRKABLZMVQVONJVSVRABCDOPVTVOVQVTAB
      QFZGFFZRSZVOTUAUBZVQTUAUBZSZVOVQSZABCDUCWEWBEFZTUAUBZUDUBZWEUETUAUBZUDUBZ
      SWIWKSZWFWCWEWIWKWEVQVPABCDUFZUGZUHUIZWIWHWBAWABCUJDUFZUGZUHUIUEUKULUMWDW
      JWEWLABCDUNWLWEUEUDUBWEWKUEWEUDUOUPWEWPUQURUSWMWHUESZWCUEWHIJZUEUEIJWMWSU
      TWBVALWTWQWBVBVCUEVDVEWHUEWRVDVFVGWBWQVHVIVJUEVOIJZUEVQIJZWFWGUTAVALXADAV
      BVCVPVALXBWNVPVBVCVOVQADUGZWOVFVGVKVLVQVOWOXCVMVN $.
      $( [27-Oct-1999] $)
  $}

  $( The norm of the projection is less than or equal to the norm. $)
  pjnorm $p |- ( ( H e. CH /\ A e. ~H ) ->
                ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` A ) ) $=
    ( cch wcel chil cpj cfv cno cle wbr cif c0v wceq fveq2 fveq1d fveq2d
    breq1d breq12d helch elimel ax-hv0cl pjnormi dedth2h ) BCDZAEDZABFGZGZHGZAH
    GZIJAUDBEKZFGZGZHGZUIIJUEALKZUKGZHGZUNHGZIJBAELBUJMZUHUMUIIURUGULHURAUFUKBU
    JFNOPQAUNMZUMUPUIUQIUSULUOHAUNUKNPAUNHNRUNUJBECSTALEUATUBUC $.
    $( [28-Oct-1999] $)

  $( Pythagorean theorem for projectors. $)
  pjpyth $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( normh ` A ) ^ 2 ) =
               ( ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) +
                 ( ( normh ` ( ( proj ` ( _|_ ` H ) ) ` A ) ) ^ 2 ) ) ) $=
    ( cch wcel chil cno cfv c2 cexp co cpj cort caddc wceq cif c0v fveq2
    fveq1d fveq2d opreq1d opreq12d eqeq2d eqeq12d helch elimel ax-hv0cl
    pjpythi dedth2h ) BCDZAEDZAFGZHIJZABKGZGZFGZHIJZABLGZKGZGZFGZHIJZMJZNULAUIB
    EOZKGZGZFGZHIJZAVCLGZKGZGZFGZHIJZMJZNUJAPOZFGZHIJZVNVDGZFGZHIJZVNVIGZFGZHIJ
    ZMJZNBAEPBVCNZVBVMULWDUPVGVAVLMWDUOVFHIWDUNVEFWDAUMVDBVCKQRSTWDUTVKHIWDUSVJ
    FWDAURVIWDUQVHKBVCLQSRSTUAUBAVNNZULVPVMWCWEUKVOHIAVNFQTWEVGVSVLWBMWEVFVRHIW
    EVEVQFAVNVDQSTWEVKWAHIWEVJVTFAVNVIQSTUAUCVNVCBECUDUEAPEUFUEUGUH $.
    $( [11-Apr-2006] $)

  $( If a vector does not belong to subspace, the norm of its projection is
     less than its norm. $)
  pjnel $p |- ( ( H e. CH /\ A e. ~H ) -> ( -. A e. H <->
              ( normh ` ( ( proj ` H ) ` A ) ) < ( normh ` A ) ) ) $=
    ( cch wcel chil wn cpj cfv cno clt wbr wb cif c0v wceq eleq2 notbid fveq2
    fveq1d fveq2d breq1d bibi12d eleq1 breq12d helch elimel ax-hv0cl pjneli
    dedth2h ) BCDZAEDZABDZFZABGHZHZIHZAIHZJKZLAUJBEMZDZFZAUSGHZHZIHZUQJKZLUKANM
    ZUSDZFZVFVBHZIHZVFIHZJKZLBAENBUSOZUMVAURVEVMULUTBUSAPQVMUPVDUQJVMUOVCIVMAUN
    VBBUSGRSTUAUBAVFOZVAVHVEVLVNUTVGAVFUSUCQVNVDVJUQVKJVNVCVIIAVFVBRTAVFIRUDUBV
    FUSBECUEUFANEUGUFUHUI $.
    $( [2-Nov-1999] $)

  $( A vector belongs to the subspace of a projection iff the norm of its
     projection equals its norm.  This and ~ pjch yield Theorem 26.3 of
     [Halmos] p. 44. $)
  pjnorm2 $p |- ( ( H e. CH /\ A e. ~H ) -> ( A e. H <->
              ( normh ` ( ( proj ` H ) ` A ) ) = ( normh ` A ) ) ) $=
    ( cch wcel chil wa cpj cfv cno wceq cle wbr clt wn cr wb pjhcl normcl syl
    adantl eqlelt syl11anc pjnorm biantrurd pjnel con1bid 3bitr2rd ) BCDZAEDZFZ
    ABGHHZIHZAIHZJZULUMKLZULUMMLZNZFZUQABDZUJULODZUMODZUNURPUJUKEDUTABQUKRSUIVA
    UHARTULUMUAUBUJUOUQABUCUDUJUSUPABUEUFUG $.
    $( [7-Apr-2001] $)

