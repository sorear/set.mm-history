$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Projectors (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    pjorth.1 $e |- A e. ~H $.
    pjorth.2 $e |- B e. ~H $.
    $( Projection components on orthocomplemented subspaces are orthogonal. $)
    pjorthi $p |- ( H e. CH ->
          ( ( ( proj ` H ) ` A ) .ih ( ( proj ` ( _|_ ` H ) ) ` B ) ) = 0 ) $=
      ( cch wcel csh cpj cfv cort wa csp co cc0 wceq chsh chil axpjcl mpan2 syl
      choccl jca shocorth sylc ) CFGZCHGACIJJZCGZBCKJZIJJZUIGZLUGUJMNOPCQUFUHUK
      UFARGUHDACSTUFUIFGZUKCUBULBRGUKEBUISTUAUCUGUJCUDUE $.
      $( [26-Oct-1999] $)
  $}

  $( Property of identity projection.  Remark in [Beran] p. 111. $)
  pjch1 $p |- ( A e. ~H -> ( ( proj ` ~H ) ` A ) = A ) $=
    ( chil wcel cpj cfv wceq wb c0v cif eleq1 fveq2 id eqeq12d bibi12d ax-hv0cl
    helch elimel pjchi dedth ibi ) ABCZABDEZEZAFZUAUAUDGUAAHIZBCZUEUBEZUEFZGAHA
    UEFZUAUFUDUHAUEBJUIUCUGAUEAUEUBKUILMNUEBPAHBOQRST $.
    $( [28-Oct-1999] $)

  $( The orthogonal projection.  Lemma 4.4(i) of [Beran] p. 111. $)
  pjo $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( proj ` ( _|_ ` H ) ) ` A ) =
             ( ( ( proj ` ~H ) ` A ) -h ( ( proj ` H ) ` A ) ) ) $=
    ( cch wcel chil wa cpj cfv cmv co cort cva pjch1 adantl axpjpj eqtr2d helch
    wceq wb pjhcl pjcli choccl sylan hvsubadd syl3anc mpbird eqcomd ) BCDZAEDZF
    ZAEGHHZABGHHZIJZABKHZGHHZUJUMUORZULUOLJZUKRZUJUKAUQUIUKARUHAMNABOPUJUKEDZUL
    EDUOEDZUPURSUIUSUHAEQUANABTUHUNCDUIUTBUBAUNTUCUKULUOUDUEUFUG $.
    $( [30-Oct-1999] $)

  ${
    pjidm.1 $e |- H e. CH $.
    $( Component of a projection.  (Revised by Mario Carneiro, 19-May-2014.) $)
    pjcompi $p |- ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
                 ( ( proj ` H ) ` ( A +h B ) ) = A ) $=
      ( wcel cort cfv wa cva co cpj wceq cch chil sseli choccli hvaddcl sylancr
      chssii axpjcl syl2an axpjpj eqid simpl simpr chocunii syl22anc mpan2i mpd
      wi simpld ) ACEZBCFGZEZHZABIJZCKGGZALZUPUMKGGZBLZUOUPUQUSIJLZURUTHZUOCMEZ
      UPNEZVADULANEBNEVDUNCNACDSOUMNBUMCDPZSOABQUAZUPCUBRUOVAUPUPLZVBUPUCUOUQCE
      ZUSUMEZULUNVAVGHVBUJUOVCVDVHDVFUPCTRUOUMMEVDVIVEVFUPUMTRULUNUDULUNUEUQUSA
      BUPCDUFUGUHUIUK $.
      $( [19-May-2014] $) $( [31-Oct-1999] $)
    pjidm.2 $e |- A e. ~H $.
    $( A projection is idempotent.  Property (ii) of [Beran] p. 109. $)
    pjidmi $p |- ( ( proj ` H ) ` ( ( proj ` H ) ` A ) ) =
               ( ( proj ` H ) ` A ) $=
      ( cpj cfv wcel wceq pjclii pjhclii pjchi mpbi ) ABEFZFZBGNMFNHABCDINBCABC
      DJKL $.
      $( [28-Oct-1999] $)

    ${
      pjadj.3 $e |- B e. ~H $.
      $( A projection is self-adjoint.  Property (i) of [Beran] p. 109. $)
      pjadjii $p |- ( ( ( proj ` H ) ` A ) .ih B ) =
                  ( A .ih ( ( proj ` H ) ` B ) ) $=
        ( cpj cfv cva co csp caddc ccj cc0 wcel wceq pjorthi ax-mp pjhclii chil
        cort cch fveq2i eqtri choccli his1i 3eqtr4ri opreq2i his7 mp3an ax-his2
        cj0 3eqtr4i pjpji opreq1i ) ACGHZHZBUPHZBCUAHZGHZHZIJZKJZUQAUTHZIJZURKJ
        ZUQBKJAURKJUQURKJZUQVAKJZLJZVGVDURKJZLJZVCVFVHVJVGLURVDKJZMHZNVJVHVMNMH
        NVLNMCUBOZVLNPDBACFEQRUCULUDVDURAUSCDUEZESZBCDFSZUFVNVHNPDABCEFQRUGUHUQ
        TOZURTOZVATOVCVIPACDESZVQBUSVOFSUQURVAUIUJVRVDTOVSVFVKPVTVPVQUQVDURUKUJ
        UMBVBUQKBCDFUNUHAVEURKACDEUNUOUM $.
        $( [30-Oct-1999] $)

      $( Projection of vector sum is sum of projections. $)
      pjaddii $p |- ( ( proj ` H ) ` ( A +h B ) ) =
                  ( ( ( proj ` H ) ` A ) +h ( ( proj ` H ) ` B ) ) $=
        ( cva co cpj cfv cort pjpji pjhclii eqtri wcel csh chshii shaddcl mp3an
        pjclii opreq12i choccli hvadd4i fveq2i wceq pjcompi mp2an ) ABGHZCIJZJA
        UIJZBUIJZGHZACKJZIJZJZBUNJZGHZGHZUIJZULUHURUIUHUJUOGHZUKUPGHZGHURAUTBVA
        GACDELBCDFLUAUJUOUKUPACDEMAUMCDUBZEMBCDFMBUMVBFMUCNUDULCOZUQUMOZUSULUEC
        POUJCOUKCOVCCDQACDETBCDFTUJUKCRSUMPOUOUMOUPUMOVDUMVBQAUMVBETBUMVBFTUOUP
        UMRSULUQCDUFUGN $.
        $( [31-Oct-1999] $)
    $}

    $( The inner product of a projection and its argument is the square of the
       norm of the projection.  Remark in [Halmos] p. 44. $)
    pjinormii $p |- ( ( ( proj ` H ) ` A ) .ih A ) =
                 ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) $=
      ( cpj cfv cno c2 cexp csp pjhclii normsqi pjadjii pjidmi opreq1i 3eqtr2ri
      co ) ABEFZFZGFHIQSSJQSRFZAJQSAJQSABCDKZLSABCUADMTSAJABCDNOP $.
      $( [13-Aug-2000] $)

    ${
      pjmul.3 $e |- C e. CC $.
      $( Projection of (scalar) product is product of projection. $)
      pjmulii $p |- ( ( proj ` H ) ` ( C .h A ) ) =
                  ( C .h ( ( proj ` H ) ` A ) ) $=
        ( csm co cpj cfv cort cva pjhclii eqtri csh chshii pjclii shmulcl mp3an
        wcel pjpji opreq2i choccli hvdistr1i fveq2i wceq cc pjcompi mp2an ) BAG
        HZCIJZJBAUKJZGHZBACKJZIJJZGHZLHZUKJZUMUJUQUKUJBULUOLHZGHUQAUSBGACDEUAUB
        BULUOFACDEMAUNCDUCZEMUDNUEUMCTZUPUNTZURUMUFCOTBUGTZULCTVACDPFACDEQBULCR
        SUNOTVCUOUNTVBUNUTPFAUNUTEQBUOUNRSUMUPCDUHUIN $.
        $( [31-Oct-1999] $)
    $}

    ${
      pjsub.3 $e |- B e. ~H $.
      $( Projection of vector difference is difference of projections. $)
      pjsubii $p |- ( ( proj ` H ) ` ( A -h B ) ) =
                  ( ( ( proj ` H ) ` A ) -h ( ( proj ` H ) ` B ) ) $=
        ( c1 cneg csm co cva cpj cfv cmv ax-1cn negcli hvmulcli pjaddii pjhclii
        hvsubvali pjmulii opreq2i eqtri fveq2i 3eqtr4i ) AGHZBIJZKJZCLMZMZAUIMZ
        UFBUIMZIJZKJZABNJZUIMUKULNJUJUKUGUIMZKJUNAUGCDEUFBGOPZFQRUPUMUKKBUFCDFU
        QUAUBUCUOUHUIABEFTUDUKULACDESBCDFSTUE $.
        $( [31-Oct-1999] $)
    $}

    ${
      pjsslem.1 $e |- G e. CH $.
      $( Lemma for subset relationships of projections. $)
      pjsslem $p |- ( ( ( proj ` ( _|_ ` H ) ) ` A ) -h
                      ( ( proj ` ( _|_ ` G ) ) ` A ) ) =
                    ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) $=
        ( cort cfv cpj cmv co c0v chil cch wcel wceq pjo mp2an pjhclii 3eqtri
        c1 cneg opreq12i helch pjclii hvsubsub4i hvsubid ax-mp opreq1i hvsubcli
        csm hv2negi hvnegdii ) ACGHIHHZABGHIHHZJKZLACIHHZABIHHZJKZJKZUAUBUSUKKU
        RUQJKUPAMIHHZUQJKZVAURJKZJKVAVAJKZUSJKUTUNVBUOVCJCNOAMOZUNVBPDEACQRBNOV
        EUOVCPFEABQRUCVAUQVAURAMUDEUEZACDESZVFABFESZUFVDLUSJVAMOVDLPVFVAUGUHUIT
        USUQURVGVHUJULUQURVGVHUMT $.
        $( [31-Oct-1999] $)

      $( Subset relationship for projections.  Theorem 4.5(i)->(ii) of [Beran]
         p. 112. $)
      pjss2i $p |- ( H C_ G -> ( ( proj ` H ) ` ( ( proj ` G ) ` A ) ) =
                 ( ( proj ` H ) ` A ) ) $=
        ( wss cort cfv cpj cmv wcel wceq choccli pjclii chsscon3i ssel pjhclii
        co c0v mpi chshii shsubcl mp3an1 sylancr pjsslem eleq1i hvsubcli pjoc2i
        sylbi bitri pjsubii eqeq1i hvsubeq0i 3bitri pjidmi eqeq2i bitr2i sylibr
        csh ) CBGZACHIZJIIZABHIZJIIZKSZVBLZABJIIZCJIZIZAVIIZMZVAVCVBLZVEVBLZVGA
        VBCDNZEOVAVDVBGZVNCBDFPVPVEVDLVNAVDBFNEOVDVBVEQUAUJVBUTLVMVNVGVBVOUBVCV
        EVBUCUDUEVGVJVKVIIZMZVLVGVHVKKSZVIIZTMZVJVQKSZTMVRVGVSVBLWAVFVSVBABCDEF
        UFUGVSCDVHVKABFERZACDERZUHUIUKVTWBTVHVKCDWCWDULUMVJVQVHCDWCRVKCDWDRUNUO
        VQVKVJACDEUPUQURUS $.
        $( [31-Oct-1999] $)

      $( Projection meet property.  Remark in [Kalmbach] p. 66.  Also Theorem
         4.5(i)->(iv) of [Beran] p. 112. $)
      pjssmii $p |- ( H C_ G -> ( ( ( proj ` G ) ` A )
      -h ( ( proj ` H ) ` A ) ) = ( ( proj ` ( G i^i ( _|_ ` H ) ) ) ` A ) ) $=
        ( wss cort cfv cpj cmv co csm cva pjhclii c0v wceq ax-mp wcel pjclii c1
        cin cneg choccli chincli pjsubii hvsubvali inss1 pjss2i chshii shococss
        csh inss2 chsscon3i sstri sselii pjoc2i opreq2i cc ax-1cn negcli hvmul0
        mpbi opreq12i chil ax-hvaddid 3eqtri wa ssel mpi shsubcl mp3an1 sylancr
        eqtri pjsslem sylbi syl5eqelr elin hvsubcli pjchi bitr3i sylib syl5reqr
        jca ) CBGZABCHIZUBZJIZIZABJIIZACJIIZKLZWHIZWLWMWJWHIZWKWHIZKLWNUAUCZWOM
        LZNLZWIWJWKWGBWFFCDUDZUEZABFEOZACDEOZUFWNWOWJWGWTXAOWKWGWTXBOUGWRWIPNLZ
        WIWNWIWQPNWGBGWNWIQBWFUHABWGWTEFUIRWQWPPMLZPWOPWPMWKWGHIZSWOPQCXEWKCWFH
        IZXECULSCXFGCDUJCUKRWGWFGXFXEGBWFUMWGWFWTWSUNVCUOACDETZUPWKWGWTXBUQVCUR
        WPUSSXDPQUAUTVAWPVBRVNVDWIVESXCWIQAWGWTEOWIVFRVNVGWEWLBSZWLWFSZVHZWMWLQ
        ZWEXHXIWEWJBSZWKBSZXHABFETWEWKCSXMXGCBWKVIVJBULSXLXMXHBFUJWJWKBVKVLVMWE
        WLAWFJIIZABHIZJIIZKLZWFABCDEFVOWEXOWFGZXQWFSZCBDFUNXRXNWFSZXPWFSZXSAWFW
        SETXRXPXOSYAAXOBFUDETXOWFXPVIVJWFULSXTYAXSWFWSUJXNXPWFVKVLVMVPVQWDXJWLW
        GSXKWLBWFVRWLWGWTWJWKXAXBVSVTWAWBWC $.
        $( [31-Oct-1999] $)

      $( Theorem 4.5(iv)->(v) of [Beran] p. 112. $)
      pjssge0ii $p |- ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) )
                    = ( ( proj ` ( G i^i ( _|_ ` H ) ) ) ` A ) ->
   0 <_ ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) .ih A ) ) $=
        ( cpj cfv cmv co cort cin wceq cc0 cno c2 cexp csp cle choccli chincli
        pjhclii normcli sqge0i opreq1 pjinormii syl6eq syl5breqr ) ABGHHACGHHIJ
        ZABCKHZLZGHHZMZNULOHZPQJZUIARJZSUNULAUKBUJFCDTUAZEUBUCUDUMUPULARJUOUIUL
        ARUEAUKUQEUFUGUH $.
        $( [13-Aug-2000] $)

      $( Theorem 4.5(v)<->(vi) of [Beran] p. 112. $)
      pjdifnormii $p |- ( 0 <_
                 ( ( ( ( proj ` G ) ` A ) -h ( ( proj ` H ) ` A ) ) .ih A ) <->
      ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` ( ( proj ` G ) ` A ) ) ) $=
        ( cc0 cpj cfv cno c2 cexp co cmin cle wbr csp pjhclii chil wcel normcli
        cmv resqcli subge0i wceq his2sub mp3an pjinormii opreq12i eqtri normge0
        breq2i wb ax-mp le2sqi mp2an 3bitr4i ) GABHIIZJIZKLMZACHIIZJIZKLMZNMZOP
        VCUTOPZGURVAUBMAQMZOPVBUSOPZUTVCUSURABFERZUAZUCVBVAACDERZUAZUCUDVFVDGOV
        FURAQMZVAAQMZNMZVDURSTZVASTZASTVFVNUEVHVJEURVAAUFUGVLUTVMVCNABFEUHACDEU
        HUIUJULGVBOPZGUSOPZVGVEUMVPVQVJVAUKUNVOVRVHURUKUNVBUSVKVIUOUPUQ $.
        $( [13-Aug-2000] $)

      $( The projection on a subspace join is the sum of the projections. $)
      pjcji $p |- ( H C_ ( _|_ ` G ) -> ( ( proj ` ( H vH G ) ) ` A ) =
                ( ( ( proj ` H ) ` A ) +h ( ( proj ` G ) ` A ) ) ) $=
        ( cort cfv wss cpj cva chj cmv choccli chil wcel pjhclii opreq2i eqtr4i
        co cin pjssmii opreq2d c1 cneg csm wceq hvaddsub12 mp3an pjpoi hvnegdii
        3eqtr4i hvsubcli hvsubvali chjcomi chdmm4i fveq2i chincli pjopi 3eqtr4g
        fveq1i eqtri eqcomd ) CBGHZIZACJHHZABJHHZKTZACBLTZJHZHZVEAAVDJHHZVFMTZM
        TZAAVDCGHZUAZJHHZMTZVHVKVEVMVQAMAVDCDEBFNZUBUCVHAUDUEVMUFTZKTZVNVFAVLMT
        ZKTZAVFVLMTZKTZVHWAVFOPAOPVLOPWCWEUGACDEQZEAVDVSEQZVFAVLUHUIVGWBVFKABFE
        UJRVTWDAKVLVFWGWFUKRULAVMEVLVFWGWFUMUNSVKAVPGHZJHZHVRAVJWIVIWHJVIBCLTWH
        CBDFUOBCFDUPSUQVAAVPVDVOVSCDNUREUSVBUTVC $.
        $( [1-Nov-1999] $)
    $}
  $}

  ${
    pjadjt.1 $e |- H e. CH $.
    $( A projection is self-adjoint.  Property (i) of [Beran] p. 109. $)
    pjadji $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( ( proj ` H ) ` A ) .ih B ) =
                  ( A .ih ( ( proj ` H ) ` B ) ) ) $=
      ( chil wcel cpj cfv csp co wceq c0v fveq2 opreq1d opreq1 eqeq12d ax-hv0cl
      cif opreq2 elimel opreq2d pjadjii dedth2h ) AEFZBEFZACGHZHZBIJZABUFHZIJZK
      UDALRZUFHZBIJZUKUIIJZKULUEBLRZIJZUKUOUFHZIJZKABLLAUKKZUHUMUJUNUSUGULBIAUK
      UFMNAUKUIIOPBUOKZUMUPUNURBUOULISUTUIUQUKIBUOUFMUAPUKUOCDALEQTBLEQTUBUC $.
      $( [6-Oct-2000] $)

    $( Projection of vector sum is sum of projections. $)
    pjaddi $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( proj ` H ) ` ( A +h B ) ) =
                ( ( ( proj ` H ) ` A ) +h ( ( proj ` H ) ` B ) ) ) $=
      ( chil wcel cva cpj cfv wceq c0v cif opreq1 fveq2d fveq2 opreq1d ax-hv0cl
      co eqeq12d elimel opreq2 opreq2d pjaddii dedth2h ) AEFZBEFZABGRZCHIZIZAUH
      IZBUHIZGRZJUEAKLZBGRZUHIZUMUHIZUKGRZJUMUFBKLZGRZUHIZUPURUHIZGRZJABKKAUMJZ
      UIUOULUQVCUGUNUHAUMBGMNVCUJUPUKGAUMUHOPSBURJZUOUTUQVBVDUNUSUHBURUMGUANVDU
      KVAUPGBURUHOUBSUMURCDAKEQTBKEQTUCUD $.
      $( [14-Nov-2000] $)

    $( The inner product of a projection and its argument is the square of the
       norm of the projection.  Remark in [Halmos] p. 44. $)
    pjinormi $p |- ( A e. ~H -> ( ( ( proj ` H ) ` A ) .ih A ) =
                 ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) ) $=
      ( chil wcel cpj cfv csp co cno c2 cexp wceq c0v cif fveq2 opreq12d fveq2d
      id opreq1d eqeq12d ax-hv0cl elimel pjinormii dedth ) ADEZABFGZGZAHIZUHJGZ
      KLIZMUFANOZUGGZULHIZUMJGZKLIZMANAULMZUIUNUKUPUQUHUMAULHAULUGPZUQSQUQUJUOK
      LUQUHUMJURRTUAULBCANDUBUCUDUE $.
      $( [2-Jun-2006] $)

    $( Projection of vector difference is difference of projections. $)
    pjsubi $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( proj ` H ) ` ( A -h B ) ) =
                ( ( ( proj ` H ) ` A ) -h ( ( proj ` H ) ` B ) ) ) $=
      ( chil wcel cmv cpj cfv wceq c0v cif opreq1 fveq2d fveq2 opreq1d ax-hv0cl
      co eqeq12d elimel opreq2 opreq2d pjsubii dedth2h ) AEFZBEFZABGRZCHIZIZAUH
      IZBUHIZGRZJUEAKLZBGRZUHIZUMUHIZUKGRZJUMUFBKLZGRZUHIZUPURUHIZGRZJABKKAUMJZ
      UIUOULUQVCUGUNUHAUMBGMNVCUJUPUKGAUMUHOPSBURJZUOUTUQVBVDUNUSUHBURUMGUANVDU
      KVAUPGBURUHOUBSUMURCDAKEQTBKEQTUCUD $.
      $( [14-Nov-2000] $)

    $( Projection of scalar product is scalar product of projection. $)
    pjmuli $p |- ( ( A e. CC /\ B e. ~H ) -> ( ( proj ` H ) ` ( A .h B ) ) =
                ( A .h ( ( proj ` H ) ` B ) ) ) $=
      ( cc wcel chil csm co cpj cfv cc0 cif opreq1 fveq2d eqeq12d opreq2 elimel
      wceq c0v fveq2 opreq2d ax-hv0cl 0cn pjmulii dedth2h ) AEFZBGFZABHIZCJKZKZ
      ABUJKZHIZSUGALMZBHIZUJKZUNULHIZSUNUHBTMZHIZUJKZUNURUJKZHIZSABLTAUNSZUKUPU
      MUQVCUIUOUJAUNBHNOAUNULHNPBURSZUPUTUQVBVDUOUSUJBURUNHQOVDULVAUNHBURUJUAUB
      PURUNCDBTGUCRALEUDRUEUF $.
      $( [26-Nov-2000] $)

    $( The inner product of a projection and its argument is nonnegative. $)
    pjige0i $p |- ( A e. ~H -> 0 <_ ( ( ( proj ` H ) ` A ) .ih A ) ) $=
      ( chil wcel cc0 cpj cfv cno c2 cexp co csp cle cr wbr pjhcli normcl sqge0
      3syl pjinormi breqtrrd ) ADEZFABGHHZIHZJKLZUDAMLNUCUDDEUEOEFUFNPABCQUDRUE
      STABCUAUB $.
      $( [2-Jun-2006] $)
  $}

  $( The inner product of a projection and its argument is nonnegative. $)
  pjige0 $p |- ( ( H e. CH /\ A e. ~H ) ->
                0 <_ ( ( ( proj ` H ) ` A ) .ih A ) ) $=
    ( cch wcel chil cc0 cpj cfv csp co cle wbr wi c0h wceq fveq2 fveq1d opreq1d
    cif breq2d imbi2d h0elch elimel pjige0i dedth imp ) BCDZAEDZFABGHZHZAIJZKLZ
    UGUHULMUHFAUGBNSZGHZHZAIJZKLZMBNBUMOZULUQUHURUKUPFKURUJUOAIURAUIUNBUMGPQRTU
    AAUMBNCUBUCUDUEUF $.
    $( [2-Jun-2006] $)

  $( The projection on a subspace join is the sum of the projections. $)
  pjcjt2 $p |- ( ( H e. CH /\ G e. CH /\ A e. ~H ) ->
            ( H C_ ( _|_ ` G ) -> ( ( proj ` ( H vH G ) ) ` A ) =
            ( ( ( proj ` H ) ` A ) +h ( ( proj ` G ) ` A ) ) ) ) $=
    ( cch wcel chil cort cfv wss chj co cpj cva wi cif c0v fveq1d fveq2 eqeq12d
    wceq sseq1 opreq1 fveq2d opreq1d imbi12d sseq2d opreq2 opreq2d imbi2d helch
    opreq12d elimel ax-hv0cl pjcji dedth3h ) CDEZBDEZAFEZCBGHZIZACBJKZLHZHZACLH
    ZHZABLHZHZMKZTZNUPCFOZUSIZAVJBJKZLHZHZAVJLHZHZVGMKZTZNVJUQBFOZGHZIZAVJVSJKZ
    LHZHZVPAVSLHZHZMKZTZNWAURAPOZWCHZWIVOHZWIWEHZMKZTZNCBAFFPCVJTZUTVKVIVRCVJUS
    UAWOVCVNVHVQWOAVBVMWOVAVLLCVJBJUBUCQWOVEVPVGMWOAVDVOCVJLRQUDSUEBVSTZVKWAVRW
    HWPUSVTVJBVSGRUFWPVNWDVQWGWPAVMWCWPVLWBLBVSVJJUGUCQWPVGWFVPMWPAVFWEBVSLRQUH
    SUEAWITZWHWNWAWQWDWJWGWMAWIWCRWQVPWKWFWLMAWIVORAWIWERUKSUIWIVSVJCFDUJULAPFU
    MULBFDUJULUNUO $.
    $( [1-Nov-1999] $)

  ${
    pj0.1 $e |- H e. CH $.
    $( The projection of the zero vector. $)
    pj0i $p |- ( ( proj ` H ) ` 0h ) = 0h $=
      ( c0v cort cfv wcel cpj wceq csh chshii oc0 ax-mp ax-hv0cl pjoc2i mpbi )
      CADEFZCAGEECHAIFPABJAKLCABMNO $.
      $( [31-Oct-1999] $)
  $}

  $( Projection of a vector in the projection subspace.  Lemma 4.4(ii) of
     [Beran] p. 111. $)
  pjch $p |- ( ( H e. CH /\ A e. ~H ) ->
              ( A e. H <-> ( ( proj ` H ) ` A ) = A ) ) $=
    ( cch wcel chil cpj cfv wceq wb cif eleq2 fveq2 fveq1d eqeq1d bibi12d eleq1
    c0v id eqeq12d elimel helch ax-hv0cl pjchi dedth2h ) BCDZAEDZABDZABFGZGZAHZ
    IAUEBEJZDZAUKFGZGZAHZIUFAQJZUKDZUPUMGZUPHZIBAEQBUKHZUGULUJUOBUKAKUTUIUNAUTA
    UHUMBUKFLMNOAUPHZULUQUOUSAUPUKPVAUNURAUPAUPUMLVARSOUPUKBECUATAQEUBTUCUD $.
    $( [30-Oct-1999] $)

  $( The projection of a vector in the projection subspace is itself. $)
  pjid $p |- ( ( H e. CH /\ A e. H ) -> ( ( proj ` H ) ` A ) = A ) $=
    ( cch wcel chil wa cpj cfv wceq simpl chel jca pjch biimpa sylancom ) BCDZA
    BDZPAEDZFZABGHHAIZPQFPRPQJABKLSQTABMNO $.
    $( [9-Apr-2006] $)

  ${
    $d x H $.
    $( The set of vectors belonging to the subspace of a projection.  Part of
       Theorem 26.2 of [Halmos] p. 44. $)
    pjvec $p |- ( H e. CH -> H = { x e. ~H | ( ( proj ` H ) ` x ) = x } ) $=
      ( cch wcel chil cin cv cpj cfv wceq crab wss chss sseqin2 sylib rabbi2dva
      pjch eqtr3d ) BCDZEBFZBAGZBHIIUAJZAEKSBELTBJBMBENOSUBAEBUABQPR $.
      $( [11-Apr-2006] $)

    $( The set of vectors belonging to the orthocomplemented subspace of a
       projection.  Second part of Theorem 27.3 of [Halmos] p. 45. $)
    pjocvec $p |- ( H e. CH -> ( _|_ ` H ) =
                   { x e. ~H | ( ( proj ` H ) ` x ) = 0h } ) $=
      ( cch wcel chil cort cfv cin cv cpj c0v wceq crab wss choccl chss sseqin2
      syl sylib pjoc2 rabbi2dva eqtr3d ) BCDZEBFGZHZUDAIZBJGGKLZAEMUCUDENZUEUDL
      UCUDCDUHBOUDPRUDEQSUCUGAEUDUFBTUAUB $.
      $( [24-Apr-2006] $)
  $}

  ${
    pjocin.1 $e |- G e. CH $.
    pjocin.2 $e |- H e. CH $.
    $( Membership of projection in orthocomplement of intersection. $)
    pjocini $p |- ( A e. ( _|_ ` ( G i^i H ) ) ->
                 ( ( proj ` G ) ` A ) e. ( _|_ ` ( G i^i H ) ) ) $=
      ( cin cort cfv wcel cpj cmv cch chil wceq chincli choccli cheli pjpo wss
      co sylancr pjcli inss1 chsscon3i mpbi sseli 3syl csh chshii shsubcl mpdan
      mp3an1 eqeltrd ) ABCFZGHZIZABJHHZAABGHZJHHZKTZUOUPBLIAMIZUQUTNDAUOUNBCDEO
      ZPZQZABRUAUPUSUOIZUTUOIZUPVAUSURIVEVDAURBDPUBURUOUSUNBSURUOSBCUCUNBVBDUDU
      EUFUGUOUHIUPVEVFUOVCUIAUSUOUJULUKUM $.
      $( [21-Apr-2001] $)

    $( Membership of projection in an intersection. $)
    pjini $p |- ( A e. ( G i^i H ) ->
                 ( ( proj ` G ) ` A ) e. ( G i^i H ) ) $=
      ( cin wcel cpj cfv wceq inss1 sseli cch pjid mpan syl eleq1d ibir ) ABCFZ
      GZABHIIZSGTUAASTABGZUAAJZSBABCKLBMGUBUCDABNOPQR $.
      $( [22-Apr-2001] $)
  $}

  ${
    $d x y z w G $.  $d x y z w H $.
    pjjs.1 $e |- G e. CH $.
    pjjs.2 $e |- H e. SH $.
    $( A sufficient condition for subspace join to be equal to subspace sum. $)
    pjjsi $p |- ( A. x e. ( G vH H ) ( ( proj ` ( _|_ ` G ) ) ` x ) e. H ->
               ( G vH H ) = ( G +H H ) ) $=
      ( vw vy vz cv cort cfv cpj wcel co wss wa wceq cva wrex sylibr wral fveq2
      chj cph eleq1d rcla4v chil chshii shjcli cheli pjcli anim1i axpjpj adantr
      cch mpan jca sylan rcla4eopr 3expa syl shseli ex syld com12 ssrdv shsleji
      jctir eqss ) AIZBJKLKZKZCMZABCUCNZUAZVNBCUDNZOZVPVNOZPVNVPQVOVQVRVOFVNVPF
      IZVNMZVOVSVPMZVTVOVSVKKZCMZWAVMWCAVSVNVJVSQVLWBCVJVSVKUBUEUFVTWCWAVTWCPZV
      SGIHIRNQHCSGBSZWAWDVSBLKKZBMZWCPZVSWFWBRNQZPZWEVTVSUGMZWCWJVSVNBCBDUHZEUI
      UJWKWCPWHWIWKWGWCVSBDUKULWKWIWCBUOMWKWIDVSBUMUPUNUQURWGWCWIWEGHBCWFWBVSRU
      SUTVAGHBCVSWLEVBTVCVDVEVFBCWLEVGVHVNVPVIT $.
      $( [29-May-2004] $)
  $}

  ${
    $d x y z H $.
    pjfn.1 $e |- H e. CH $.
    $( Functionality of a projection. $)
    pjfni $p |- ( proj ` H ) Fn ~H $=
      ( vx vy vz cv cva wceq cort cfv wrex crio cvv wcel cpj chil wfn cch cmpt
      co pjfval ax-mp fnmpt riotaex a1i mprg ) CFZDFEFGTHEAIJKZDALZMNZAOJZPQCPC
      PUIUKMARNUKCPUISHBCEDAUAUBUCUJUGPNUHDAUDUEUF $.
      $( [23-Dec-2013] $) $( [30-Oct-1999] $)

    $( The range of a projection.  Part of Theorem 26.2 of [Halmos] p. 44. $)
    pjrni $p |- ran ( proj ` H ) = H $=
      ( vx vy cv chil wcel cpj cfv wceq wa copab crn wex cab rnopab pjfni dffn5
      wfn eleq1 mpbi rneqi cheli cch pjid mpan eqcomd vex eqeq2d anbi12d cla4ev
      fveq2 syl2anc pjcli syl5ibr impcom exlimiv impbii abbi2i 3eqtr4i ) CEZFGZ
      DEZVAAHIZIZJZKZCDLZMVGCNZDOVDMAVGCDPVDVHVDFSVDVHJABQCDFVDRUAUBVIDAVCAGZVI
      VJVCFGZVCVCVDIZJZVIVCABUCVJVLVCAUDGVJVLVCJBVCAUEUFUGVGVKVMKCVCDUHVAVCJZVB
      VKVFVMVAVCFTVNVEVLVCVAVCVDULUIUJUKUMVGVJCVFVBVJVBVJVFVEAGVAABUNVCVEATUOUP
      UQURUSUT $.
      $( [30-Oct-1999] $)

    $( A projection maps onto its subspace. $)
    pjfoi $p |- ( proj ` H ) : ~H -onto-> H $=
      ( chil cpj cfv wfo wfn crn wceq df-fo pjfni pjrni mpbir2an ) CAADEZFNCGNH
      AICANJABKABLM $.
      $( [24-Apr-2006] $)

    $( The mapping of a projection. $)
    pjfi $p |- ( proj ` H ) : ~H --> ~H $=
      ( chil cpj cfv wf wfn crn wss df-f pjfni pjrni chssii eqsstri mpbir2an )
      CCADEZFPCGPHZCICCPJABKQACABLABMNO $.
      $( [11-Nov-2000] $)

    $( The value of a projection in terms of components. $)
    pjvi $p |- ( ( A e. H /\ B e. ( _|_ ` H ) ) ->
               ( ( proj ` H ) ` ( A +h B ) ) = A ) $=
      ( wcel cort cfv wa cva cpj c0v chil wceq cheli choccli pjaddi syl2an pjid
      co cch mpan wb pjoc2 sylancr ibi opreqan12d ax-hvaddid syl adantr 3eqtrd
      ) ACEZBCFGZEZHABISCJGZGZAUNGZBUNGZISZAKISZAUKALEZBLEZUOURMUMACDNZBULCDONZ
      ABCDPQUKUMUPAUQKICTEZUKUPAMDACRUAUMUQKMZUMVDVAUMVEUBDVCBCUCUDUEUFUKUSAMZU
      MUKUTVFVBAUGUHUIUJ $.
      $( [28-Nov-2000] $)
  $}

  $( A projection maps onto its subspace. $)
  pjfo $p |- ( H e. CH -> ( proj ` H ) : ~H -onto-> H ) $=
    ( cch wcel chil cpj cfv wfo c0h cif wceq wb fveq2 foeq1 foeq3 h0elch elimel
    syl pjfoi dedth2v ) ABCZDAAEFZGZDATAHIZEFZGZDUCUDGAAHHAUCJUAUDJUBUEKAUCELDA
    UAUDMQAUCDUDNUCAHBOPRS $.
    $( [24-Apr-2006] $)

  $( The range of a projection.  Part of Theorem 26.2 of [Halmos] p. 44. $)
  pjrn $p |- ( H e. CH -> ran ( proj ` H ) = H ) $=
    ( cch wcel chil cpj cfv wfo crn wceq pjfo forn syl ) ABCDAAEFZGMHAIAJDAMKL
    $.
    $( [24-Apr-2006] $)

  $( The mapping of a projection. $)
  pjf $p |- ( H e. CH -> ( proj ` H ) : ~H --> ~H ) $=
    ( cch wcel chil cpj cfv wf wss wfo pjfo fof syl chss fss syl2anc ) ABCZDAAE
    FZGZADHDDQGPDAQIRAJDAQKLAMDADQNO $.
    $( [24-Apr-2006] $)

  $( Functionality of a projection. $)
  pjfn $p |- ( H e. CH -> ( proj ` H ) Fn ~H ) $=
    ( cch wcel chil cpj cfv wf wfn pjf ffn syl ) ABCDDAEFZGLDHAIDDLJK $.
    $( [30-May-2006] $)

  ${
    pjsumt.1 $e |- G e. CH $.
    pjsumt.2 $e |- H e. CH $.
    $( The projection on a subspace sum is the sum of the projections. $)
    pjsumi $p |- ( A e. ~H ->
            ( G C_ ( _|_ ` H ) -> ( ( proj ` ( G +H H ) ) ` A ) =
            ( ( ( proj ` G ) ` A ) +h ( ( proj ` H ) ` A ) ) ) ) $=
      ( chil wcel cort cfv wss cph co cpj cva wceq wa chj osumi fveq2d cch imp
      fveq1d adantl wi pjcjt2 mp3an12 eqtrd ex ) AFGZBCHIJZABCKLZMIZIZABMIIACMI
      INLZOUIUJPUMABCQLZMIZIZUNUJUMUQOUIUJAULUPUJUKUOMBCDERSUBUCUIUJUQUNOZBTGCT
      GUIUJURUDDEACBUEUFUAUGUH $.
      $( [11-Nov-2000] $)

    $( One-to-one correspondence of projection and subspace. $)
    pj11i $p |- ( ( proj ` G ) = ( proj ` H ) <-> G = H ) $=
      ( cpj cfv wceq crn rneq pjrni 3eqtr3g fveq2 impbii ) AEFZBEFZGZABGPNHOHAB
      NOIACJBDJKABELM $.
      $( [26-Nov-2000] $)

    $( Vector decomposition into sum of projections on orthogonal subspaces. $)
    pjdsi $p |- ( ( A e. ( G vH H ) /\ G C_ ( _|_ ` H ) ) ->
               A = ( ( ( proj ` G ) ` A ) +h ( ( proj ` H ) ` A ) ) ) $=
      ( chj co wcel cort cfv wss wa cph cpj cva osumi fveq2d fveq1d cch wceq
      chjcli pjid mpan sylan9eqr chil cheli pjsumi imp sylan eqtr3d ) ABCFGZHZB
      CIJKZLABCMGZNJZJZAABNJJACNJJOGZUMULUPAUKNJZJZAUMAUOURUMUNUKNBCDEPQRUKSHUL
      USATBCDEUAZAUKUBUCUDULAUEHZUMUPUQTZAUKUTUFVAUMVBABCDEUGUHUIUJ $.
      $( [21-Jun-2006] $)
  $}

  ${
    pjds3.1 $e |- F e. CH $.
    pjds3.2 $e |- G e. CH $.
    pjds3.3 $e |- H e. CH $.
    $( Vector decomposition into sum of projections on orthogonal subspaces. $)
    pjds3i $p |- ( ( ( A e. ( ( F vH G ) vH H ) /\ F C_ ( _|_ ` G ) ) /\
     ( F C_ ( _|_ ` H ) /\ G C_ ( _|_ ` H ) ) ) -> A = ( ( ( ( proj ` F ) ` A )
                +h ( ( proj ` G ) ` A ) ) +h ( ( proj ` H ) ` A ) ) ) $=
      ( chj co wcel cort cfv wss wa cpj cva cph wceq chjcli opreq1d simpl pjdsi
      choccli chlubii syl2an osumi fveq2d fveq1d ad2antlr chil cheli pjsumi imp
      sylan adantr 3eqtr2d ) ABCHIZDHIZJZBCKLMZNZBDKLZMCVBMNZNAAUQOLZLZADOLLZPI
      ZABCQIZOLZLZVFPIZABOLLACOLLPIZVFPIZVAUSUQVBMAVGRVCUSUTUABCVBEFDGUCUDAUQDB
      CEFSZGUBUEUTVKVGRUSVCUTVJVEVFPUTAVIVDUTVHUQOBCEFUFUGUHTUIVAVKVMRVCVAVJVLV
      FPUSAUJJZUTVJVLRZAURUQDVNGSUKVOUTVPABCEFULUMUNTUOUP $.
      $( [22-Jun-2006] $)
  $}

  $( One-to-one correspondence of projection and subspace. $)
  pj11 $p |- ( ( G e. CH /\ H e. CH ) ->
              ( ( proj ` G ) = ( proj ` H ) <-> G = H ) ) $=
    ( cch wcel cpj cfv wceq wb c0h cif fveq2 eqeq1d eqeq1 bibi12d eqeq2d h0elch
    eqeq2 elimel pj11i dedth2h ) ACDZBCDZAEFZBEFZGZABGZHUAAIJZEFZUDGZUGBGZHUHUB
    BIJZEFZGZUGUKGZHABIIAUGGZUEUIUFUJUOUCUHUDAUGEKLAUGBMNBUKGZUIUMUJUNUPUDULUHB
    UKEKOBUKUGQNUGUKAICPRBICPRST $.
    $( [24-Apr-2006] $)

  ${
    $d h x y z $.
    $( Functionality of the projection function. $)
    pjmfn $p |- proj Fn CH $=
      ( vx vz vy vh chil cv cva co wceq cort cfv wrex crio cmpt cvv cpj cch wfn
      wcel df-pj fnmpt ax-hilex mptex a1i mprg ) AEAFBFCFGHICDFZJKLBUFMZNZOSZPQ
      RDQDQUHPOACBDTUAUIUFQSAEUGUBUCUDUE $.
      $( [24-Apr-2006] $)

    $( The projector function maps one-to-one into the set of Hilbert space
       operators. $)
    pjmf1 $p |- proj : CH -1-1-> ( ~H ^m ~H ) $=
      ( vx vy cch chil cmap co cpj wf1 wf cv cfv wceq wral dff13 wfn wcel ffnfv
      wi ax-hilex mpbir2an pjmfn pjf elmap sylibr rgen wa pj11 biimpd rgen2a )
      CDDEFZGHCUJGIZAJZGKZBJZGKLZULUNLZRZBCMACMABCUJGNUKGCOUMUJPZACMACUJGQUAURA
      CULCPZDDUMIURULUBDDUMSSUCUDUETUQABCUSUNCPUFUOUPULUNUGUHUIT $.
      $( [24-Apr-2006] $)
  $}

  $( The inner product of projections on orthogonal subspaces vanishes. $)
  pjoi0 $p |- ( ( ( G e. CH /\ H e. CH /\ A e. ~H ) /\ G C_ ( _|_ ` H ) ) ->
               ( ( ( proj ` G ) ` A ) .ih ( ( proj ` H ) ` A ) ) = 0 ) $=
    ( cch wcel chil w3a cort cfv wss cpj crn wceq pjrn adantr wfn pjfn fnfvelrn
    wa sylan csp co cc0 fveq2d adantl sseq12d biimpar 3adantl3 csh eqeltrd chsh
    id syl 3ad2ant2 simpr 3adant2 3adant1 jca shorth syl3c syldan ) BDEZCDEZAFE
    ZGZBCHIZJZBKIZLZCKIZLZHIZJZAVHIZAVJIZUAUBUCMZVBVCVGVMVDVBVCSZVMVGVQVIBVLVFV
    BVIBMVCBNOVCVLVFMVBVCVKCHCNZUDUEUFUGUHVEVMSVKUIEZVMVNVIEZVOVKEZSZVPVEVSVMVC
    VBVSVDVCVKDEVSVCVKCDVRVCULUJVKUKUMUNOVEVMUOVEWBVMVEVTWAVBVDVTVCVBVHFPVDVTBQ
    FAVHRTUPVCVDWAVBVCVJFPVDWACQFAVJRTUQUROVNVOVIVKUSUTVA $.
    $( [30-Jun-2006] $)

  ${
    pjoi0.1 $e |- G e. CH $.
    pjoi0.2 $e |- H e. CH $.
    pjoi0.3 $e |- A e. ~H $.
    $( The inner product of projections on orthogonal subspaces vanishes. $)
    pjoi0i $p |- ( G C_ ( _|_ ` H ) ->
                ( ( ( proj ` G ) ` A ) .ih ( ( proj ` H ) ` A ) ) = 0 ) $=
      ( cch wcel chil w3a cort cfv wss cpj csp co cc0 wceq 3pm3.2i pjoi0 mpan )
      BGHZCGHZAIHZJBCKLMABNLLACNLLOPQRUBUCUDDEFSABCTUA $.
      $( [1-Nov-1999] $)

    $( Pythagorean theorem for projections on orthogonal subspaces. $)
    pjopythi $p |- ( G C_ ( _|_ ` H ) ->
       ( ( normh ` ( ( ( proj ` G ) ` A ) +h ( ( proj ` H ) ` A ) ) ) ^ 2 ) =
                 ( ( ( normh ` ( ( proj ` G ) ` A ) ) ^ 2 ) +
                   ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) ) ) $=
      ( cort cfv wss cpj csp co cc0 wceq cva cno c2 cexp caddc pjhclii pjoi0i
      normpythi syl ) BCGHIABJHHZACJHHZKLMNUDUEOLPHQRLUDPHQRLUEPHQRLSLNABCDEFUA
      UDUEABDFTACEFTUBUC $.
      $( [1-Nov-1999] $)
  $}

  $( Pythagorean theorem for projections on orthogonal subspaces. $)
  pjopyth $p |- ( ( H e. CH /\ G e. CH /\ A e. ~H ) -> ( H C_ ( _|_ ` G ) ->
       ( ( normh ` ( ( ( proj ` H ) ` A ) +h ( ( proj ` G ) ` A ) ) ) ^ 2 ) =
                 ( ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) +
                   ( ( normh ` ( ( proj ` G ) ` A ) ) ^ 2 ) ) ) ) $=
    ( cch wcel chil cort cfv cpj cva co cno c2 cexp caddc wceq wi fveq2 opreq1d
    fveq2d wss cif c0v sseq1 fveq1d eqeq12d imbi12d sseq2d opreq2d imbi2d helch
    opreq12d elimel ax-hv0cl pjopythi dedth3h ) CDEZBDEZAFEZCBGHZUAZACIHZHZABIH
    ZHZJKZLHZMNKZVCLHZMNKZVELHZMNKZOKZPZQUQCFUBZUTUAZAVOIHZHZVEJKZLHZMNKZVRLHZM
    NKZVLOKZPZQVOURBFUBZGHZUAZVRAWFIHZHZJKZLHZMNKZWCWJLHZMNKZOKZPZQWHUSAUCUBZVQ
    HZWRWIHZJKZLHZMNKZWSLHZMNKZWTLHZMNKZOKZPZQCBAFFUCCVOPZVAVPVNWECVOUTUDXJVHWA
    VMWDXJVGVTMNXJVFVSLXJVCVRVEJXJAVBVQCVOIRUEZSTSXJVJWCVLOXJVIWBMNXJVCVRLXKTSS
    UFUGBWFPZVPWHWEWQXLUTWGVOBWFGRUHXLWAWMWDWPXLVTWLMNXLVSWKLXLVEWJVRJXLAVDWIBW
    FIRUEZUITSXLVLWOWCOXLVKWNMNXLVEWJLXMTSUIUFUGAWRPZWQXIWHXNWMXCWPXHXNWLXBMNXN
    WKXALXNVRWSWJWTJAWRVQRZAWRWIRZULTSXNWCXEWOXGOXNWBXDMNXNVRWSLXOTSXNWNXFMNXNW
    JWTLXPTSULUFUJWRVOWFCFDUKUMBFDUKUMAUCFUNUMUOUP $.
    $( [2-Nov-1999] $)

  ${
    pjnorm.1 $e |- H e. CH $.
    pjnorm.2 $e |- A e. ~H $.
    $( The norm of the projection is less than or equal to the norm. $)
    pjnormi $p |- ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` A ) $=
      ( cpj cfv cno cort cva co cle chil wcel csp cc0 wceq wbr pjhclii choccli
      wa pm3.2i cch pjorthi ax-mp normpyc mp2 pjpji fveq2i breqtrri ) ABEFFZGFZ
      UJABHFZEFFZIJZGFZAGFKUJLMZUMLMZTUJUMNJOPZUKUOKQUPUQABCDRAULBCSDRUABUBMURC
      AABDDUCUDUJUMUEUFAUNGABCDUGUHUI $.
      $( [27-Oct-1999] $)

    $( Pythagorean theorem for projections. $)
    pjpythi $p |- ( ( normh ` A ) ^ 2 ) =
                 ( ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) +
                   ( ( normh ` ( ( proj ` ( _|_ ` H ) ) ` A ) ) ^ 2 ) ) $=
      ( cno cfv c2 cexp co cpj cort cva caddc pjpji fveq2i opreq1i wss wceq csh
      ax-mp wcel chshii shococss choccli pjopythi eqtri ) AEFZGHIABJFFZABKFZJFF
      ZLIZEFZGHIZUHEFGHIUJEFGHIMIZUGULGHAUKEABCDNOPBUIKFQZUMUNRBSUAUOBCUBBUCTAB
      UICBCUDDUETUF $.
      $( [27-Oct-1999] $)

    $( If a vector does not belong to subspace, the norm of its projection is
       less than its norm. $)
    pjneli $p |- ( -. A e. H <->
                ( normh ` ( ( proj ` H ) ` A ) ) < ( normh ` A ) ) $=
      ( cno cfv cpj cle wbr wcel wceq c2 cexp co caddc cc0 normcli chil normge0
      ax-mp wne wa clt pjnormi biantrur cort c0v pjoc1i pjhclii resqcli choccli
      wn recni 0cn sqcli addcani pjpythi sq0 opreq2i addid1i eqtr2i eqeq12i 0re
      wb leidi sq11i norm-i-i bitr2i 3bitr4ri 3bitri necon3bbii ltleni 3bitr4i
      mp2an ) AEFZABGFFZEFZUAZVQVOHIZVRUBABJZULVQVOUCIVSVRABCDUDUEVTVOVQVTABUFF
      ZGFFZUGKZVOLMNZVQLMNZKZVOVQKZABCDUHWEWBEFZLMNZONZWEPLMNZONZKWIWKKZWFWCWEW
      IWKWEVQVPABCDUIZQZUJUMZWIWHWBAWABCUKDUIZQZUJUMPUNUOUPWDWJWEWLABCDUQWLWEPO
      NWEWKPWEOURUSWEWPUTVAVBWMWHPKZWCPWHHIZPPHIWMWSVDWBRJWTWQWBSTPVCVEWHPWRVCV
      FVNWBWQVGVHVIPVOHIZPVQHIZWFWGVDARJXADASTVPRJXBWNVPSTVOVQADQZWOVFVNVJVKVQV
      OWOXCVLVM $.
      $( [27-Oct-1999] $)
  $}

  $( The norm of the projection is less than or equal to the norm. $)
  pjnorm $p |- ( ( H e. CH /\ A e. ~H ) ->
                ( normh ` ( ( proj ` H ) ` A ) ) <_ ( normh ` A ) ) $=
    ( cch wcel chil cpj cfv cno cle wbr cif c0v wceq fveq2 fveq1d fveq2d breq1d
    breq12d helch elimel ax-hv0cl pjnormi dedth2h ) BCDZAEDZABFGZGZHGZAHGZIJAUD
    BEKZFGZGZHGZUIIJUEALKZUKGZHGZUNHGZIJBAELBUJMZUHUMUIIURUGULHURAUFUKBUJFNOPQA
    UNMZUMUPUIUQIUSULUOHAUNUKNPAUNHNRUNUJBECSTALEUATUBUC $.
    $( [28-Oct-1999] $)

  $( Pythagorean theorem for projectors. $)
  pjpyth $p |- ( ( H e. CH /\ A e. ~H ) -> ( ( normh ` A ) ^ 2 ) =
               ( ( ( normh ` ( ( proj ` H ) ` A ) ) ^ 2 ) +
                 ( ( normh ` ( ( proj ` ( _|_ ` H ) ) ` A ) ) ^ 2 ) ) ) $=
    ( cch wcel chil cno cfv c2 cexp co cpj cort caddc wceq cif c0v fveq2 fveq1d
    fveq2d opreq1d opreq12d eqeq2d eqeq12d elimel ax-hv0cl pjpythi dedth2h
    helch ) BCDZAEDZAFGZHIJZABKGZGZFGZHIJZABLGZKGZGZFGZHIJZMJZNULAUIBEOZKGZGZFG
    ZHIJZAVCLGZKGZGZFGZHIJZMJZNUJAPOZFGZHIJZVNVDGZFGZHIJZVNVIGZFGZHIJZMJZNBAEPB
    VCNZVBVMULWDUPVGVAVLMWDUOVFHIWDUNVEFWDAUMVDBVCKQRSTWDUTVKHIWDUSVJFWDAURVIWD
    UQVHKBVCLQSRSTUAUBAVNNZULVPVMWCWEUKVOHIAVNFQTWEVGVSVLWBMWEVFVRHIWEVEVQFAVNV
    DQSTWEVKWAHIWEVJVTFAVNVIQSTUAUCVNVCBECUHUDAPEUEUDUFUG $.
    $( [11-Apr-2006] $)

  $( If a vector does not belong to subspace, the norm of its projection is
     less than its norm. $)
  pjnel $p |- ( ( H e. CH /\ A e. ~H ) -> ( -. A e. H <->
              ( normh ` ( ( proj ` H ) ` A ) ) < ( normh ` A ) ) ) $=
    ( cch wcel chil wn cpj cfv cno clt wbr wb cif c0v wceq notbid fveq2 bibi12d
    fveq2d elimel eleq2 fveq1d breq1d eleq1 breq12d ax-hv0cl pjneli dedth2h
    helch ) BCDZAEDZABDZFZABGHZHZIHZAIHZJKZLAUJBEMZDZFZAUSGHZHZIHZUQJKZLUKANMZU
    SDZFZVFVBHZIHZVFIHZJKZLBAENBUSOZUMVAURVEVMULUTBUSAUAPVMUPVDUQJVMUOVCIVMAUNV
    BBUSGQUBSUCRAVFOZVAVHVEVLVNUTVGAVFUSUDPVNVDVJUQVKJVNVCVIIAVFVBQSAVFIQUERVFU
    SBECUITANEUFTUGUH $.
    $( [2-Nov-1999] $)

  $( A vector belongs to the subspace of a projection iff the norm of its
     projection equals its norm.  This and ~ pjch yield Theorem 26.3 of
     [Halmos] p. 44. $)
  pjnorm2 $p |- ( ( H e. CH /\ A e. ~H ) -> ( A e. H <->
              ( normh ` ( ( proj ` H ) ` A ) ) = ( normh ` A ) ) ) $=
    ( cch wcel chil wa cpj cfv cno cle wbr clt wn cr wb pjhcl normcl syl adantl
    wceq eqlelt syl2anc pjnorm biantrurd pjnel con1bid 3bitr2rd ) BCDZAEDZFZABG
    HHZIHZAIHZTZULUMJKZULUMLKZMZFZUQABDZUJULNDZUMNDZUNUROUJUKEDUTABPUKQRUIVAUHA
    QSULUMUAUBUJUOUQABUCUDUJUSUPABUEUFUG $.
    $( [7-Apr-2001] $)

