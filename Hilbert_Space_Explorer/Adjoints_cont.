$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Adjoints (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g h u v w y z A $.  $d u B $.  $d f C $.  $d f t w x z F $.
    $d f v w x z G $.  $d f g h t u v w x y z T $.
    cnlnadjlem.1 $e |- T e. LinOp $.
    cnlnadjlem.2 $e |- T e. ConOp $.
    cnlnadjlem.3 $e |- G =
                    { <. g , h >. | ( g e. ~H /\ h = ( ( T ` g ) .ih y ) ) } $.
    $( Lemma for ~ cnlnadji (Theorem 3.10 of [Beran] p. 104: every continuous
       linear operator has an adjoint).  The value of the auxiliary functional
       ` G ` . $)
    cnlnadjlem1 $p |- ( A e. ~H -> ( G ` A ) = ( ( T ` A ) .ih y ) ) $=
      ( cv cfv csp co chil wceq fveq2 opreq1d oprex fvopab4 ) DEBDJZCKZAJZLMBCK
      ZUBLMNFTBOUAUCUBLTBCPQIUCUBLRS $.
      $( [16-Feb-2006] $)

    $( Lemma for ~ cnlnadji . ` G ` is a continuous linear functional. $)
    cnlnadjlem2 $p |- ( y e. ~H -> ( G e. LinFn /\ G e. ConFn ) ) $=
      ( vx vz chil wcel cc co cfv cmul wceq csp cle cr vw cv clf ccnf csm caddc
      cva wral lnopfi ffvelrni hicl sylan ancoms ralrimiva fopab2 sylib hvmulcl
      wf wa w3a lnopaddi 3adant3 opreq1d id ax-his2 eqtrd 3comr sylanl2 hvaddcl
      syl3an 3expa cnlnadjlem1 syl adantll ax-his3 syl3an2 3expb adantl opreq2d
      lnopmuli ad2antll 3eqtr4rd opreqan12d ralrimivva ellnfn sylanbrc cabs cno
      3eqtr4d wbr wrex cnop nmcopexi normcl remulcl sylancr adantr abscl syl2an
      eqeltrd bcs eqbrtrd cc0 normge0 jca nmcoplbi lemul1a syl31anc letrd recnd
      fveq2d recni mp3an1 breqtrd opreq1 breq2d ralbidv rcla4ev syl2anc lnfncon
      mul32 wb mpbird ) AUBZKLZEUCLZEUDLZYEKMEURZIUBZUAUBZUENZJUBZUGNZEOZYIYJEO
      ZPNZYLEOZUFNZQZJKUHZUAKUHIMUHYFYECUBZBOZYDRNZMLZCKUHYHYEUUDCKUUAKLZYEUUDU
      UEUUBKLYEUUDKKUUABBFUIZUJUUBYDUKULUMUNCDKMUUCEHUOUPYEYTIUAMKYEYIMLZYJKLZU
      SZUSZYSJKUUJYLKLZUSYMBOZYDRNZYKBOZYDRNZYLBOZYDRNZUFNZYNYRUUIYEYKKLZUUKUUM
      UURQZYIYJUQZYEUUSUUKUUTUUSUUKYEUUTUUSUUKYEUTZUUMUUNUUPUGNZYDRNZUURUVBUULU
      VCYDRUUSUUKUULUVCQYEYKYLBFVAVBVCUUSUUNKLUUKUUPKLZYEYEUVDUURQKKYKBUUFUJKKY
      LBUUFUJZYEVDUUNUUPYDVEVJVFVGVKVHUUIUUKYNUUMQZYEUUIUUKUSYMKLZUVGUUIUUSUUKU
      VHUVAYKYLVIULAYMBCDEFGHVLVMVNUUJUUKYPUUOYQUUQUFUUJYIYJBOZUENZYDRNZYIUVIYD
      RNZPNZUUOYPYEUUGUUHUVKUVMQZUUGUUHYEUVNUUHUUGUVIKLYEUVNKKYJBUUFUJYIUVIYDVO
      VPVGVQUUIUUOUVKQYEUUIUUNUVJYDRYIYJBFVTVCVRUUHYPUVMQYEUUGUUHYOUVLYIPAYJBCD
      EFGHVLVSWAWBAYLBCDEFGHVLZWCWIUNWDIUAJEWEWFZYEYGYQWGOZYIYLWHOZPNZSWJZJKUHZ
      ITWKZYEBWLOZYDWHOZPNZTLZUVQUWEUVRPNZSWJZJKUHZUWBYEUWCTLZUWDTLZUWFBFGWMZYD
      WNZUWCUWDWOWPYEUWHJKUUKYEUWHUUKYEUSZUVQUWCUVRPNZUWDPNZUWGSUWNUVQUUPWHOZUW
      DPNZUWPUWNYQMLUVQTLUWNYQUUQMUUKYQUUQQYEUVOWQZUUKUVEYEUUQMLUVFUUPYDUKULWTY
      QWRVMUUKUWQTLZUWKUWRTLYEUUKUVEUWTUVFUUPWNVMZUWMUWQUWDWOWSUUKUWOTLZUWKUWPT
      LYEUUKUWJUVRTLUXBUWLYLWNZUWCUVRWOWPZUWMUWOUWDWOWSUWNUVQUUQWGOZUWRSUWNYQUU
      QWGUWSXKUUKUVEYEUXEUWRSWJUVFUUPYDXAULXBUWNUWTUXBUWKXCUWDSWJZUSZUWQUWOSWJZ
      UWRUWPSWJUUKUWTYEUXAWQUUKUXBYEUXDWQYEUXGUUKYEUWKUXFUWMYDXDXEVRUUKUXHYEYLB
      FGXFWQUWQUWOUWDXGXHXIUUKUVRMLZUWDMLZUWPUWGQZYEUUKUVRUXCXJYEUWDUWMXJUWCMLU
      XIUXJUXKUWCUWLXLUWCUVRUWDYAXMWSXNUMUNUWAUWIIUWETYIUWEQZUVTUWHJKUXLUVSUWGU
      VQSYIUWEUVRPXOXPXQXRXSYEYFYGUWBYBUVPIJEXTVMYCXE $.
      $( [16-Feb-2006] $)

    ${
      cnlnadjlem.4 $e |- B = U. { w e. ~H | A. v e. ~H ( ( T ` v ) .ih y )
                         = ( v .ih w ) } $.
      cnlnadjlem.5 $e |- F = { <. y , u >. | ( y e. ~H /\ u = B ) } $.
      $( Lemma for ~ cnlnadji .  By ~ riesz4 , ` B ` is the unique vector such
         that ` ( T `` v ) .ih y ) = ( v .ih w ) ` for all ` v ` . $)
      cnlnadjlem3 $p |- ( y e. ~H -> B e. ~H ) $=
        ( cv chil wcel clf ccnf cfv csp co wceq wral crab cuni wreu cnlnadjlem2
        cin wa elin sylibr riesz4 cnlnadjlem1 eqeq1d ralbiia reubii sylib reucl
        syl syl5eqel ) APZQRZECPZFUAVCUBUCZVEBPUBUCZUDZCQUEZBQUFUGZQNVDVIBQUHZV
        JQRVDVEJUAZVGUDZCQUEZBQUHZVKVDJSTUJRZVOVDJSRJTRUKVPAFGHJKLMUIJSTULUMBCJ
        UNVAVNVIBQVMVHCQVEQRVLVFVGAVEFGHJKLMUOUPUQURUSVIBQUTVAVB $.
        $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadji .  The values of auxiliary function ` F ` are
         vectors. $)
      cnlnadjlem4 $p |- ( A e. ~H -> ( F ` A ) e. ~H ) $=
        ( chil wcel csp wceq cv co wral crab cuni opreq2 eqeq1d ralbidv rabbidv
        cfv unieqd syl5eq ax-hilex rabex uniex fvopab4 wreu reubidv clf ccnf wa
        cin cnlnadjlem2 elin sylibr riesz4 syl cnlnadjlem1 ralbiia reubii sylib
        vtoclga reucl eqeltrd ) EQRZEJUJCUAZGUJZESUBZVPBUASUBZTZCQUCZBQUDZUEZQA
        DEFWCQJAUAZETZFVQWDSUBZVSTZCQUCZBQUDZUEWCOWEWIWBWEWHWABQWEWGVTCQWEWFVRV
        SWDEVQSUFUGUHZUIUKULPWBWABQUMUNUOUPVOWABQUQZWCQRWHBQUQZWKAEQWEWHWABQWJU
        RWDQRZVPKUJZVSTZCQUCZBQUQZWLWMKUSUTVBRZWQWMKUSRKUTRVAWRAGHIKLMNVCKUSUTV
        DVEBCKVFVGWPWHBQWOWGCQVPQRWNWFVSAVPGHIKLMNVHUGVIVJVKVLWABQVMVGVN $.
        $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadji . ` F ` is an adjoint of ` T ` (later, we will
         show it is unique). $)
      cnlnadjlem5 $p |- ( ( A e. ~H /\ C e. ~H ) -> ( ( T ` C ) .ih A ) =
                        ( C .ih ( F ` A ) ) ) $=
        ( chil wcel csp vf vz co wceq cv wi fveq2 opreq1d opreq1 eqeq12d imbi2d
        cfv wral ax-17 wa copab hbopab1 hbxfreq hbfv hbopr hbral opreq2 opreq2d
        hbeq ralbidv crab fveq1i cvv ax-hilex rabex uniex eqeltri fvopab2 mpan2
        cuni syl5eq wb cbvralv rabbiia unieqi eqtri syl6req cnlnadjlem3 eqeltrd
        a1i wreu clf ccnf cin cnlnadjlem2 elin sylibr riesz4 cnlnadjlem1 eqeq1d
        syl ralbiia reubii sylib eqeq2d reuuni2 syl2anc mpbird vtoclgaf vtoclga
        ra4 com12 impcom ) GRSERSZGHULZETUCZGEKULZTUCZUDZXIUAUEZHULZETUCZXOXLTU
        CZUDZUFXIXNUFUAGRXOGUDZXSXNXIXTXQXKXRXMXTXPXJETXOGHUGUHXOGXLTUIUJUKXIXO
        RSZXSXIXSUARUMZYAXSUFXPAUEZTUCZXOYCKULZTUCZUDZUARUMZYBAUBERUBUEZESAUNZX
        SAUARYAAUNAUBUBXQXRYIXQSAUNAUBXOXLTYIXOSAUNYITSAUNAUBEKAUBKYCRSZDUEFUDU
        OZADUPZQYLADUBUQURYJUSUTVDVAYCEUDZYGXSUARYNYDXQYFXRYCEXPTVBYNYEXLXOTYCE
        KUGVCUJVEYKYHYDXOBUEZTUCZUDZUARUMZBRVFZVOZYEUDZYKYEFYTYKYEYCYMULZFYCKYM
        QVGYKFVHSUUBFUDFCUEZHULZYCTUCZUUCYOTUCZUDZCRUMZBRVFZVOZVHPUUIUUHBRVIVJV
        KVLADRFVHVMVNVPZFUUJYTPUUIYSUUHYRBRUUHYRVQYORSUUGYQCUARUUCXOUDZUUEYDUUF
        YPUULUUDXPYCTUUCXOHUGUHUUCXOYOTUIUJVRWEVSVTWAWBYKYERSYRBRWFZYHUUAVQYKYE
        FRUUKABCDFHIJKLMNOPQWCWDYKXOLULZYPUDZUARUMZBRWFZUUMYKLWGWHWISZUUQYKLWGS
        LWHSUOUURAHIJLMNOWJLWGWHWKWLBUALWMWPUUPYRBRUUOYQUARYAUUNYDYPAXOHIJLMNOW
        NWOWQWRWSYRYHBRYEYOYEUDZYQYGUARUUSYPYFYDYOYEXOTVBWTVEXAXBXCXDXSUARXFWPX
        GXEXH $.
        $( [18-Feb-2006] $)

      $( Lemma for ~ cnlnadji . ` F ` is linear. $)
      cnlnadjlem6 $p |- F e. LinOp $=
        ( wcel chil co wceq csp vx vf vz vt clo wf csm cva cfv wral cnlnadjlem3
        cv cc ellnop fopab caddc lnopfi ffvelrni adantl hvmulcl ad2antrr simplr
        wa his7 syl3anc hvaddcl sylan cnlnadjlem5 ccj cmul adantll opreq2d his5
        simpll simpr cnlnadjlem4 ad2antlr 3eqtr4d adantlr sylan2 eqtr4d 3eqtr3d
        opreq12d ralrimiva wb syl syl2an hial2eq2 syl2anc mpbid rgen2 mpbir2an
        ) IUEPQQIUFUAULZUBULZUGRZUCULZUHRZIUIZWMWNIUIZUGRZWPIUIZUHRZSZUCQUJZUBQ
        UJUAUMUJUAUBUCIUNADQQEIOABCDEFGHIJKLMNOUKUOXDUAUBUMQWMUMPZWNQPZVCZXCUCQ
        XGWPQPZVCZUDULZWRTRZXJXBTRZSZUDQUJZXCXIXMUDQXIXJQPZVCZXJFUIZWQTRZXQWOTR
        ZXQWPTRZUPRZXKXLXPXQQPZWOQPZXHXRYASXOYBXIQQXJFFKUQURZUSXGYCXHXOWMWNUTZV
        AXGXHXOVBXQWOWPVDVEXIWQQPZXOXRXKSXGYCXHYFYEWOWPVFVGZABCDWQEXJFGHIJKLMNO
        VHVGXPYAXJWTTRZXJXATRZUPRZXLXPXSYHXTYIUPXGXOXSYHSXHXGXOVCZWMVIUIZXQWNTR
        ZVJRZYLXJWSTRZVJRZXSYHYKYMYOYLVJXFXOYMYOSXEABCDWNEXJFGHIJKLMNOVHVKVLYKX
        EYBXFXSYNSXEXFXOVNZXOYBXGYDUSXEXFXOVBWMXQWNVMVEYKXEXOWSQPZYHYPSYQXGXOVO
        XFYRXEXOABCDWNEFGHIJKLMNOVPZVQWMXJWSVMVEVRVSXHXOXTYISXGABCDWPEXJFGHIJKL
        MNOVHVKWCXPXOWTQPZXAQPZXLYJSXIXOVOXGYTXHXOXFXEYRYTYSWMWSUTVTZVAXHUUAXGX
        OABCDWPEFGHIJKLMNOVPZVQXJWTXAVDVEWAWBWDXIWRQPZXBQPZXNXCWEXIYFUUDYGABCDW
        QEFGHIJKLMNOVPWFXGYTUUAUUEXHUUBUUCWTXAVFWGUDWRXBWHWIWJWDWKWL $.
        $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadji .  Helper lemma to show that ` F ` is
         continuous. $)
      cnlnadjlem7 $p |- ( A e. ~H -> ( normh ` ( F ` A ) ) <_
                        ( ( normop ` T ) x. ( normh ` A ) ) ) $=
        ( wcel cle wbr cc0 chil cfv cno cnop cmul co breq1 wne cabs cnlnadjlem4
        wa csp cc lnopfi ffvelrni syl hicl mpancom abscl normcl remulcl syl2anc
        cr nmcopexi sylancr bcs normge0 nmcoplbi lemul1a syl31anc letrd c2 cexp
        wceq normsq recnd sqval cnlnadjlem5 mpdan fveq2d hiidrcl hiidge0 eqtr2d
        jca absid 3eqtr3rd recni mul32 mp3an1 3brtr3d adantr clt wb 0re biimpar
        leltne sylan lemul1 syl112anc mpbird wf nmopge0 mulge0 mpanl12 pm2.61ne
        ax-mp ) EUAQZEJUBZUCUBZGUDUBZEUCUBZUEUFZRSZTXLRSZXITXITXLRUGXGXITUHZUKZ
        XMXIXIUEUFZXLXIUEUFZRSZXGXSXOXGXHGUBZEULUFZUIUBZXJXIUEUFZXKUEUFZXQXRRXG
        YBXTUCUBZXKUEUFZYDXGYAUMQZYBVCQXTUAQZXGYGXGXHUAQZYHABCDEFGHIJKLMNOPUJZU
        AUAXHGGLUNZUOUPZXTEUQURYAUSUPXGYEVCQZXKVCQZYFVCQXGYHYMYLXTUTUPZEUTZYEXK
        VAVBXGYCVCQZYNYDVCQXGXJVCQZXIVCQZYQGLMVDZXGYIYSYJXHUTZUPZXJXIVAVEZYPYCX
        KVAVBYHXGYBYFRSYLXTEVFURXGYMYQYNTXKRSZUKZYEYCRSZYFYDRSYOUUCXGYNUUDYPEVG
        ZWDXGYIUUFYJXHGLMVHUPYEYCXKVIVJVKXGXIVLVMUFZXHXHULUFZXQYBXGYIUUHUUIVNYJ
        XHVOUPXGXIUMQZUUHXQVNXGXIUUBVPZXIVQUPXGYBUUIUIUBZUUIXGYAUUIUIXGYIYAUUIV
        NYJABCDEFXHGHIJKLMNOPVRVSVTXGUUIVCQZTUUIRSZUULUUIVNXGYIUUMYJXHWAUPXGYIU
        UNYJXHWBUPUUIWEVBWCWFXGUUJXKUMQZYDXRVNZUUKXGXKYPVPXJUMQUUJUUOUUPXJYTWGX
        JXIXKWHWIVBWJWKXPYSXLVCQZYSTXIWLSZXMXSWMXGYSXOUUBWKZXGUUQXOXGYRYNUUQYTY
        PXJXKVAVEWKUUSXGYIXOUURYJYIUURXOYIYSTXIRSZUURXOWMZUUAXHVGTVCQYSUUTUVAWN
        TXIWPWIVBWOWQXIXLXIWRWSWTXGYNUUDXNYPUUGYRTXJRSZUUEXNYTUAUAGXAUVBYKGXBXF
        XJXKXCXDVBXE $.
        $( [18-Feb-2006] $)

      $( Lemma for ~ cnlnadji . ` F ` is continuous. $)
      cnlnadjlem8 $p |- F e. ConOp $=
        ( vz vx cfv cmul chil cco wcel cv cno co cle wral cr wrex cnop nmcopexi
        wbr cnlnadjlem7 rgen opreq1 breq2d ralbidv rcla4ev cnlnadjlem6 lnopconi
        wceq mp2an mpbir ) IUAUBPUCZIRUDRZQUCZVDUDRZSUEZUFULZPTUGZQUHUIZFUJRZUH
        UBVEVLVGSUEZUFULZPTUGZVKFKLUKVNPTABCDVDEFGHIJKLMNOUMUNVJVOQVLUHVFVLVAZV
        IVNPTVPVHVMVEUFVFVLVGSUOUPUQURVBQPIABCDEFGHIJKLMNOUSUTVC $.
        $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadji . ` F ` provides an example showing the existence
         of a continuous linear adjoint. $)
      cnlnadjlem9 $p |- E. t e. ( LinOp i^i ConOp )
             A. x e. ~H A. z e. ~H ( ( T ` x ) .ih z ) = ( x .ih ( t ` z ) ) $=
        ( wcel chil clo cco cin cv cfv csp co wceq wral cnlnadjlem6 cnlnadjlem8
        wrex mpbir2an cnlnadjlem5 ancoms rgen2a opreq2d eqeq2d 2ralbidv rcla4ev
        elin fveq1 mp2an ) LUAUBUCZSZAUDZIUECUDZUFUGZVFVGLUEZUFUGZUHZCTUIATUIZV
        HVFVGGUDZUEZUFUGZUHZCTUIATUIZGVDULVELUASLUBSLUAUBVABDEFHIJKLMNOPQRUJBDE
        FHIJKLMNOPQRUKUMVKACTVGTSVFTSVKBDEFVGHVFIJKLMNOPQRUNUOUPVQVLGLVDVMLUHZV
        PVKACTTVRVOVJVHVRVNVIVFUFVGVMLVBUQURUSUTVC $.
        $( [18-Feb-2006] $)
    $}
  $}

  ${
    $d f g h t u v w x y z T $.
    cnlnadj.1 $e |- T e. LinOp $.
    cnlnadj.2 $e |- T e. ConOp $.
    $( Every continuous linear operator has an adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadji $p |- E. t e. ( LinOp i^i ConOp )
             A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) $=
      ( vz vw vv vu vf vg vh cv cfv csp co wceq chil wral crab cuni wcel opreq2
      wa copab eqid eqeq2d ralbidv cbvrabv unieqi cnlnadjlem9 ) AGBHIJCINZDOGNZ
      PQZUMKNZPQZRZISTZKSUAZUBZDLMUNSUCJNVARUEGJUFZLNZSUCMNVCDOUNPQRUELMUFZEFVD
      UGUTUOUMHNZPQZRZISTZHSUAUSVHKHSUPVERZURVGISVIUQVFUOUPVEUMPUDUHUIUJUKVBUGU
      L $.
      $( [18-Feb-2006] $)

    $( Every continuous linear operator has a unique adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadjeui $p |- E! t e. ( LinOp i^i ConOp )
             A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) $=
      ( cv cfv csp co wceq chil wral clo cco cin wcel wa wmo wf wreu wrex adjmo
      cnlnadji inss1 sseli lnopf syl simpl eqcom 2ralbii wb lnopfi adjsym mpan2
      reu5 syl5bb biimpa jca sylan immoi ax-mp mpbir2an ) AGZDHBGZIJZVDVECGZHIJ
      ZKZBLMALMZCNOPZUAVJCVKUBVGVKQZVJRZCSZVJCVKUPABCDEFUDLLVGTZVDVEDHIJVDVGHVE
      IJKBLMALMZRZCSVNABCDUCVMVQCVLVOVJVQVLVGNQVOVKNVGNOUEUFVGUGUHVOVJRVOVPVOVJ
      UIVOVJVPVJVHVFKZBLMALMZVOVPVIVRABLLVFVHUJUKVOLLDTVSVPULDEUMABVGDUNUOUQURU
      SUTVAVBVC $.
      $( [18-Feb-2006] $)
  $}

  ${
    $d t x y T $.
    $( Every continuous linear operator has a unique adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadjeu $p |- ( T e. ( LinOp i^i ConOp ) -> E! t e. ( LinOp i^i ConOp )
           A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) ) $=
      ( clo cco cin wcel cv cfv csp wceq chil wral wreu ch0o cif fveq1 sselii
      co opreq1d eqeq1d 2ralbidv reubidv inss1 elin 0lnop 0cnop mpbir2an elimel
      inss2 cnlnadjeui dedth ) DEFGZHZAIZDJZBIZKTZUPURCIJKTZLZBMNAMNZCUNOUPUODP
      QZJZURKTZUTLZBMNAMNZCUNODPDVCLZVBVGCUNVHVAVFABMMVHUSVEUTVHUQVDURKUPDVCRUA
      UBUCUDABCVCUNEVCEFUEDPUNPUNHPEHPFHPEFUFUGUHUIUJZSUNFVCEFUKVISULUM $.
      $( [19-Feb-2006] $)

    $( Every continuous linear operator has an adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadj $p |- ( T e. ( LinOp i^i ConOp ) -> E. t e. ( LinOp i^i ConOp )
           A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) ) $=
      ( clo cco cin wcel cv cfv csp co wceq chil wral wreu cnlnadjeu reurex syl
      wrex ) DEFGZHAIZDJBIZKLUBUCCIJKLMBNOANOZCUAPUDCUATABCDQUDCUARS $.
      $( [18-Feb-2006] $)
  $}

  ${
    $d t u v x y z $.
    $( Every continuous linear Hilbert space operator has an adjoint. $)
    cnlnssadj $p |- ( LinOp i^i ConOp ) C_ dom adjh $=
      ( vy vt vx vz vu vv clo cco cado cv wcel wex cfv csp co wceq chil wral wf
      w3a cin cdm cop wa wrex cnlnadj df-rex sylib inss1 sseli lnopf syl a1d wi
      a1i adantrd eqcom biimpi ralimi adjsym syl2an ancoms syl5ib expimpd 3jcad
      wb copab dfadj2 eleq2i vex fveq1 opreq2d eqeq1d 2ralbidv 3anbi13d opreq1d
      feq1 eqeq2d 3anbi23d opelopab bitr2i syl6ib eximdv mpd eldm2 sylibr ssriv
      ) AGHUAZIUBZAJZWHKZWJBJZUCZIKZBLZWJWIKWKWLWHKZCJZWJMDJZNOZWQWRWLMNOZPZDQR
      ZCQRZUDZBLZWOWKXCBWHUEXECDBWJUFXCBWHUGUHWKXDWNBWKXDQQWJSZQQWLSZWQWRWJMZNO
      ZWQWLMZWRNOZPZDQRCQRZTZWNWKXDXFXGXMWKXFXDWKWJGKXFWHGWJGHUIZUJWJUKULZUMWKW
      PXGXCWPXGUNWKWPWLGKXGWHGWLXOUJWLUKULZUOUPWKWPXCXMXCWTWSPZDQRZCQRZWKWPUDXM
      XBXSCQXAXRDQXAXRWSWTUQURUSUSWPWKXTXMVFZWPXGXFYAWKXQXPCDWLWJUTVAVBVCVDVEWN
      WMQQEJZSZQQFJZSZWQWRYBMZNOZWQYDMZWRNOZPZDQRCQRZTZEFVGZKXNIYMWMCDFEVHVIYLX
      FYEXIYIPZDQRCQRZTXNEFWJWLAVJZBVJYBWJPZYCXFYKYOYEQQYBWJVQYQYJYNCDQQYQYGXIY
      IYQYFXHWQNWRYBWJVKVLVMVNVOYDWLPZYEXGYOXMXFQQYDWLVQYRYNXLCDQQYRYIXKXIYRYHX
      JWRNWQYDWLVKVPVRVNVSVTWAWBWCWDBWJIYPWEWFWG $.
      $( [18-Feb-2006] $)
  $}


  $( Every bounded linear Hilbert space operator has an adjoint. $)
  bdopssadj $p |- BndLinOp C_ dom adjh $=
    ( cbo clo cco cin cado cdm lncnbd cnlnssadj eqsstr3i ) ABCDEFGHI $.
    $( [19-Feb-2006] $)

  $( Every bounded linear Hilbert space operator has an adjoint. $)
  bdopadj $p |- ( T e. BndLinOp -> T e. dom adjh ) $=
    ( cbo cado cdm bdopssadj sseli ) BCDAEF $.
    $( [22-Feb-2006] $)

  ${
    $d t w x y z T $.
    $( The adjoint of a bounded linear operator is a bounded linear
       operator. $)
    adjbdln $p |- ( T e. BndLinOp -> ( adjh ` T ) e. BndLinOp ) $=
      ( vx vy vt cbo wcel cfv cv csp co wceq chil wral crab cuni wf wa ax-hilex
      cab wb cado cmap df-rab elmap anbi1i abbii eqtri a1i wrex wmo clo cco cin
      unieqi cnlnadj lncnopbd lncnbd rexeq ax-mp adjmo ax-17 bdopf adjsym sylan
      3imtr3i pm5.32da eqcom 2ralbii anbi12i syl6rbbr mobid mpbiri sylibr ssriv
      wss mouniss mp3an1 syl2anc adjval2 3eqtr4rd wreu cnlnadjeu reueq1 eqeltrd
      syl reucl ) AEFZAUAGZBHZAGCHZIJZWIWJDHZGIJZKZCLMBLMZDENOZEWGWODLLUBJZNZOZ
      LLWLPZWOQZDSZOZWPWHWSXCKWGWRXBWRWLWQFZWOQZDSXBWODWQUCXEXADXDWTWOLLWLRRUDZ
      UEUFUGUNUHWGWODEUIZXEDUJZWPWSKZAUKULUMZFZWODXJUIZWGXGBCDAUOAUPZXJEKZXLXGT
      UQWODXJEURUSVEWGXHWTWIWJAGIJWIWLGWJIJKCLMBLMZQZDUJBCDAUTWGXEXPDWGDVAWGXPW
      TWMWKKZCLMBLMZQXEWGWTXOXRWGLLAPZWTXOXRTAVBZBCAWLVCVDVFXDWTWOXRXFWNXQBCLLW
      KWMVGVHVIVJVKVLEWQVOXGXHXIDEWQWLEFWTXDWLVBXFVMVNWODEWQVPVQVRWGXSWHXCKXTBC
      DAVSWEVTWGWODEWAZWPEFXKWODXJWAZWGYABCDAWBXMXNYBYATUQWODXJEWCUSVEWODEWFWEW
      D $.
      $( [19-Feb-2006] $)

    $( An operator is bounded and linear iff its adjoint is. $)
    adjbdlnb $p |- ( T e. BndLinOp <-> ( adjh ` T ) e. BndLinOp ) $=
      ( cbo wcel cado cfv adjbdln cdm bdopadj dmadjrnb sylibr wa ccom cres wceq
      cid ccnv cnvadj coeq1i funadj adantr wf1o adj1o f1ococnv1 eqtr3i a1i wfun
      ax-mp fveq1i fvco mp3an12 fvresi 3eqtr3rd adantl eqeltrd mpancom impbii )
      ABCZADEZBCZAFADGZCZUSUQUSURUTCVAURHAIJVAUSKZAURDEZBVBADDLZEZAOUTMZEZVCAVE
      VGNVBAVDVFDPZDLZVDVFVHDDQRUTUTDUAVIVFNUBUTUTDUCUGUDUHUEVAVEVCNZUSDUFZVKVA
      VJSSADDUIUJTVAVGANUSUTAUKTULUSVCBCVAURFUMUNUOUP $.
      $( [19-Feb-2006] $)

    $( The mapping of adjoints of bounded linear operators is one-to-one
       onto. $)
    adjbd1o $p |- ( adjh |` BndLinOp ) : BndLinOp -1-1-onto-> BndLinOp $=
      ( vy vx cbo cado cima cres wf1o cdm wf1 wss adj1o ax-mp wceq wb wcel wrex
      cv cfv bdopadj syl f1of1 bdopssadj f1ores mp2an wbr vex elima wfn fnbrfvb
      f1ofn rexbiia adjbdlnb eleq1 syl5bb biimpcd rexlimiv adjbdln adjadj fveq2
      mpan eqeq1d rcla4ev syl2anc impbii 3bitr2i eqriv f1oeq3 mpbi ) CDCEZDCFZG
      ZCCVJGZDHZVMDIZCVMJVKVMVMDGZVNKVMVMDUALUBVMVMCDUCUDVICMVKVLNAVICAQZVIOBQZ
      VPDUEZBCPVQDRZVPMZBCPZVPCOZBVPDCAUFZUGVTVRBCVQCOZVQVMOZVTVRNZVQSDVMUHZWEW
      FVOWGKVMVMDUJLVMVQVPDWCUIUTTUKWAWBVTWBBCVTWDWBWDVSCOVTWBVQULVSVPCUMUNUOUP
      WBVPDRZCOWHDRZVPMZWAVPUQWBVPVMOWJVPSVPURTVTWJBWHCVQWHMVSWIVPVQWHDUSVAVBVC
      VDVEVFVICCVJVGLVH $.
      $( [20-Oct-2006] $) $( [19-Feb-2006] $)

    $( The adjoint of an operator is linear.  Proposition 1 of
       [AkhiezerGlazman] p. 80. $)
    adjlnop $p |- ( T e. dom adjh -> ( adjh ` T ) e. LinOp ) $=
      ( vx vy vz vw wcel chil cfv cv co wceq wral cc wa caddc syl3anc adj2 cmul
      csp adjcl cado cdm wf csm cva clo dmadjrn dmadjop syl w3a ffvelrn 3adant3
      sylan hvmulcl adantr 3ad2ant3 simp3r hvaddcl syl3an3 ccj 3adant3l opreq2d
      his7 simp3l his5 adantrl 3adant2 3eqtr4d 3adant3r opreq12d sylan2 adantrr
      simp2 an12s eqtr4d 3eqtr3d 3com23 3expa ralrimiva syl2an anandis hial2eq2
      wb syl2anc mpbid exp32 ralrimdv ralrimivv ellnop sylanbrc ) AUAUBZFZGGAUA
      HZUCZBIZCIZUDJZDIZUEJZWMHZWOWPWMHZUDJZWRWMHZUEJZKZDGLZCGLBMLWMUFFWLWMWKFW
      NAUGWMUHUIWLXFBCMGWLWOMFZWPGFZNZXEDGWLXIWRGFZXEWLXIXJNZNZEIZWTSJZXMXDSJZK
      ZEGLZXEXLXPEGWLXKXMGFZXPWLXRXKXPWLXRXKUJZXMAHZWSSJZXTWQSJZXTWRSJZOJZXNXOX
      SXTGFZWQGFZXJYAYDKWLXRYEXKWLGGAUCXRYEAUHGGXMAUKUMZULXKWLYFXRXIYFXJWOWPUNZ
      UOUPWLXRXIXJUQXTWQWRVCPXKWLXRWSGFZYAXNKXIYFXJYIYHWQWRURUMZXMWSAQUSXSYDXMX
      BSJZXMXCSJZOJZXOXSYBYKYCYLOWLXRXIYBYKKXJWLXRXIUJZWOUTHZXTWPSJZRJZYOXMXASJ
      ZRJZYBYKYNYPYRYORWLXRXHYPYRKXGXMWPAQVAVBYNXGYEXHYBYQKWLXRXGXHVDZWLXRYEXIY
      GULWLXRXGXHUQWOXTWPVEPYNXGXRXAGFZYKYSKYTWLXRXIVMWLXIUUAXRWLXHUUAXGWPATZVF
      VGWOXMXAVEPVHVIWLXRXJYCYLKXIXMWRAQVAVJXSXRXBGFZXCGFZXOYMKWLXRXKVMWLXKUUCX
      RWLXIUUCXJXGWLXHUUCWLXHNXGUUAUUCUUBWOXAUNVKVNZVLVGWLXKUUDXRWLXJUUDXIWRATZ
      VFVGXMXBXCVCPVOVPVQVRVSXLWTGFZXDGFZXQXEWCXKWLYIUUGYJWSATVKWLXIXJUUHWLXINU
      UCUUDUUHWLXJNUUEUUFXBXCURVTWAEWTXDWBWDWEWFWGWHBCDWMWIWJ $.
      $( [17-Jun-2006] $)

    $( Every operator with an adjoint is linear. $)
    adjsslnop $p |- dom adjh C_ LinOp $=
      ( vt cado cdm clo cv wcel cfv adjadj dmadjrn adjlnop syl eqeltrrd ssriv )
      ABCZDAEZNFZOBGZBGZODOHPQNFRDFOIQJKLM $.
      $( [17-Jun-2006] $)
  $}

  ${
    $d f g h u v w z A $.  $d f g h t u v w x y z T $.
    nmopadjle.1 $e |- T e. BndLinOp $.
    $( Property of the norm of an adjoint.  Part of proof of Theorem 3.10 of
       [Beran] p. 104. $)
    nmopadjlei $p |- ( A e. ~H -> ( normh ` ( ( adjh ` T ) ` A ) ) <_
                        ( ( normop ` T ) x. ( normh ` A ) ) ) $=
      ( vz vu vv vf vw vg vh chil wcel cfv cno cv csp co wceq clo cco cado wral
      crab cuni wa cnop cmul cle cdm cbo bdopssadj sselii adjvalval mpan opreq2
      copab eqeq1d ralbidv rabbidv unieqd eqid ax-hilex rabex uniex fvopab4 cin
      eqtr4d fveq2d inss1 lncnbd eleqtrri inss2 eqeq2d cbvrabv unieqi eqbrtrd
      cnlnadjlem7 ) AKLZABUAMMZNMADOZKLEOFOZBMZVTPQZWAGOZPQZRZFKUBZGKUCZUDZRUED
      EUPZMZNMBUFMANMUGQUHVRVSWKNVRVSWBAPQZWERZFKUBZGKUCZUDZWKBUAUIZLVRVSWPRUJW
      QBUKCULFGABUMUNDEAWIWPKWJVTARZWHWOWRWGWNGKWRWFWMFKWRWCWLWEVTAWBPUOUQURUSU
      TWJVAZWOWNGKVBVCVDVEVGVHDHFEAWIBIJWJIOZKLJOWTBMVTPQRUEIJUPZSTVFZSBSTVIBUJ
      XBCVJVKZULXBTBSTVLXCULXAVAWHWCWAHOZPQZRZFKUBZHKUCWGXGGHKWDXDRZWFXFFKXHWEX
      EWCWDXDWAPUOVMURVNVOWSVQVP $.
      $( [22-Feb-2006] $)

    $( Lemma for ~ nmopadji . $)
    nmopadjlem $p |- ( normop ` ( adjh ` T ) ) <_ ( normop ` T ) $=
      ( vy chil cfv wf cnop wcel cno c1 cle wbr bdopf mp2b wa cmul co cr adantr
      cbo cado cxr cv wi wral adjbdln nmopxr ffvelrni normcl syl nmopre remulcl
      sylancr 1re remulcli a1i nmopadjlei nmopge0 pm3.2i lemul2a mp3anl3 mpanl2
      ax-mp cc0 sylan letrd recni mulid1i syl6breq ex rgen nmopub mp3an ) DDAUA
      EZFZAGEZUBHZCUCZIEZJKLZVRVNEZIEZVPKLZUDZCDUEVNGEVPKLATHZVNTHVOBAUFVNMNZWE
      DDAFZVQBAMZAUGNWDCDVRDHZVTWCWIVTOZWBVPJPQZVPKWJWBVPVSPQZWKWIWBRHZVTWIWADH
      WMDDVRVNWFUHWAUIUJSWIWLRHZVTWIVPRHZVSRHZWNWEWOBAUKVCZVRUIZVPVSULUMSWKRHWJ
      VPJWQUNUOUPWIWBWLKLVTVRABUQSWIWPVTWLWKKLZWRWPJRHZVTWSUNWPWTWOVDVPKLZOVTWS
      WOXAWQWEWGXABWHAURNUSVSJVPUTVAVBVEVFVPVPWQVGVHVIVJVKCVPVNVLVM $.
      $( [22-Feb-2006] $)

    $( Property of the norm of an adjoint.  Theorem 3.11(v) of [Beran]
       p. 106. $)
    nmopadji $p |- ( normop ` ( adjh ` T ) ) = ( normop ` T ) $=
      ( cado cfv cnop wceq cle wbr cbo wcel cr adjbdln ax-mp letri3i nmopadjlem
      nmopre cdm bdopadj adjadj fveq2i eqbrtrri mpbir2an ) ACDZEDZAEDZFUDUEGHUE
      UDGHUDUEUCIJZUDKJAIJZUFBALMZUCPMUGUEKJBAPMNABOUCCDZEDUEUDGUIAEACQJZUIAFUG
      UJBARMASMTUCUHOUAUB $.
      $( [22-Feb-2006] $)
  $}

  $( An operator is zero iff its adjoint is zero.  Theorem 3.11(i) of [Beran]
     p. 106. $)
  adjeq0 $p |- ( T = 0hop <-> ( adjh ` T ) = 0hop ) $=
    ( ch0o wceq cado cfv fveq2 adj0 syl6eq cdm cbo bdopssadj 0bdop sselii eleq1
    wcel mpbiri dmadjrnb sylibr adjadj syl a1i 3eqtr3d impbii ) ABCZADEZBCZUDUE
    BDEZBABDFGHUFUEDEZUGABUEBDFUFADIZOZUHACUFUEUIOZUJUFUKBUIOJUIBKLMUEBUINPAQRA
    STUGBCUFGUAUBUC $.
    $( [30-Jun-2008] $) $( [20-Feb-2006] $)

  ${
    $d x y A $.  $d x y T $.
    $( The adjoint of the scalar product of an operator.  Theorem 3.11(ii) of
       [Beran] p. 106. $)
    adjmul $p |- ( ( A e. CC /\ T e. dom adjh ) ->
                 ( adjh ` ( A .op T ) ) = ( ( * ` A ) .op ( adjh ` T ) ) ) $=
      ( vx vy cc wcel cado wa chil chot co wf cfv csp wceq wral dmadjop homulcl
      cv cmul cdm ccj sylan2 cjcl dmadjrn syl syl2an adj2 3expb adantll opreq2d
      csm wi ffvelrn sylan ax-his3 syl3an2 3exp exp3a imp43 simpll simprl adjcl
      ad2ant2l his52 syl3anc 3eqtr4d homval 3expa adantrr opreq1d id adantrl ex
      syl3an ralrimivv adjeq ) AEFZBGUAZFZHZIIABJKZLZIIAUBMZBGMZJKZLZCSZWBMZDSZ
      NKZWHWJWFMZNKZOZDIPCIPWBGMWFOVTVRIIBLZWCBQZABRUCVRWDEFZIIWELZWGVTAUDZVTWE
      VSFWRBUEWEQUFZWDWERUGWAWNCDIIWAWHIFZWJIFZHZWNWAXCHZAWHBMZULKZWJNKZWHWDWJW
      EMZULKZNKZWKWMXDAXEWJNKZTKZAWHXHNKZTKZXGXJXDXKXMATVTXCXKXMOZVRVTXAXBXOWHW
      JBUHUIUJUKVRVTXAXBXGXLOZVRVTXAXBXPUMVRVTXAHZXBXPXQVRXEIFZXBXPVTWOXAXRWPII
      WHBUNUOAXEWJUPUQURUSUTXDVRXAXHIFZXJXNOVRVTXCVAWAXAXBVBVTXBXSVRXAWJBVCVDAW
      HXHVEVFVGXDWIXFWJNWAXAWIXFOZXBVRVTXAXTVTVRWOXAXTWPAWHBVHUQVIVJVKXDWLXIWHN
      WAXBWLXIOZXAVRVTXBYAVRWQVTWRXBXBYAWSWTXBVLWDWJWEVHVOVIVMUKVGVNVPCDWFWBVQV
      F $.
      $( [5-Nov-2006] $) $( [21-Feb-2006] $)
  $}

  ${
    $d x y S $.  $d x y T $.
    $( The adjoint of the sum of two operators.  Theorem 3.11(iii) of [Beran]
       p. 106. $)
    adjadd $p |- ( ( S e. dom adjh /\ T e. dom adjh ) ->
                ( adjh ` ( S +op T ) ) = ( ( adjh ` S ) +op ( adjh ` T ) ) ) $=
      ( vx vy cado wcel wa chil chos co wf cfv cv wceq wral dmadjop caddc sylan
      csp syl3anc cdm hoaddcl syl2an dmadjrn syl cva ffvelrn ad2ant2r ad2ant2lr
      simprr ax-his2 adj2 3expb adantlr adantll opreq12d anim12i hosval adantrr
      eqtrd 3expa opreq1d adjcl ad2ant2rl ad2ant2l his7 3eqtr4d adantrl opreq2d
      simprl eqtr4d ralrimivva adjeq ) AEUAZFZBVNFZGZHHABIJZKZHHAELZBELZIJZKZCM
      ZVRLZDMZSJZWDWFWBLZSJZNZDHOCHOVRELWBNVOHHAKZHHBKZVSVPAPZBPZABUBUCVOHHVTKZ
      HHWAKZWCVPVOVTVNFWOAUDVTPUEZVPWAVNFWPBUDWAPUEZVTWAUBUCVQWJCDHHVQWDHFZWFHF
      ZGZGZWGWDWFVTLZWFWALZUFJZSJZWIXBWDALZWDBLZUFJZWFSJZWDXCSJZWDXDSJZQJZWGXFX
      BXJXGWFSJZXHWFSJZQJZXMXBXGHFZXHHFZWTXJXPNVOWSXQVPWTVOWKWSXQWMHHWDAUGRUHVP
      WSXRVOWTVPWLWSXRWNHHWDBUGRUIVQWSWTUJXGXHWFUKTXBXNXKXOXLQVOXAXNXKNZVPVOWSW
      TXSWDWFAULUMUNVPXAXOXLNZVOVPWSWTXTWDWFBULUMUOUPUTXBWEXIWFSVQWSWEXINZWTVQW
      KWLGWSYAVOWKVPWLWMWNUQWKWLWSYAWDABURVARUSVBXBWSXCHFZXDHFZXFXMNVQWSWTVJVOW
      TYBVPWSWFAVCVDVPWTYCVOWSWFBVCVEWDXCXDVFTVGXBWHXEWDSVQWTWHXENZWSVQWOWPGWTY
      DVOWOVPWPWQWRUQWOWPWTYDWFVTWAURVARVHVIVKVLCDWBVRVMT $.
      $( [22-Feb-2006] $)
  $}

  ${
    $d x y z S $.  $d x y z T $.
    nmoptri.1 $e |- S e. BndLinOp $.
    nmoptri.2 $e |- T e. BndLinOp $.
    $( Triangle inequality for the norms of bounded linear operators. $)
    nmoptrii $p |- ( normop ` ( S +op T ) ) <_
                  ( ( normop ` S ) + ( normop ` T ) ) $=
      ( vx chil co wf cnop cfv wcel cno cle wbr ax-mp cr wa normcl syl adantr
      chos caddc cxr cv c1 wi wral bdopf hoaddcli nmopre readdcli rexr ffvelrni
      cbo hoscli readdcl syl2anc a1i wceq hosval mp3an12 fveq2d norm-ii eqbrtrd
      cva nmoplb mp3an1 le2add mpanr12 mp2and letrd ex rgen nmopub mp3an ) FFAB
      UAGZHAIJZBIJZUBGZUCKZEUDZLJUEMNZWAVPJZLJZVSMNZUFZEFUGVPIJVSMNABAUNKZFFAHZ
      CAUHOZBUNKZFFBHZDBUHOZUIVSPKZVTVQVRWGVQPKZCAUJOZWJVRPKZDBUJOZUKZVSULOWFEF
      WAFKZWBWEWSWBQZWDWAAJZLJZWABJZLJZUBGZVSWSWDPKZWBWSWCFKXFWAABWIWLUOWCRSTWS
      XEPKZWBWSXBPKZXDPKZXGWSXAFKZXHFFWAAWIUMZXARSZWSXCFKZXIFFWABWLUMZXCRSZXBXD
      UPUQTWMWTWRURWSWDXEMNWBWSWDXAXCVEGZLJZXEMWSWCXPLWHWKWSWCXPUSWIWLWAABUTVAV
      BWSXJXMXQXEMNXKXNXAXCVCUQVDTWTXBVQMNZXDVRMNZXEVSMNZWHWSWBXRWIWAAVFVGWKWSW
      BXSWLWABVFVGWSXRXSQXTUFZWBWSXHXIYAXLXOXHXIQWNWPYAWOWQXBXDVQVRVHVIUQTVJVKV
      LVMEVSVPVNVO $.
      $( [10-Mar-2006] $)

    $( Upper bound for the norm of the composition of two bounded linear
       operators. $)
    nmopcoi $p |- ( normop ` ( S o. T ) ) <_
                   ( ( normop ` S ) x. ( normop ` T ) ) $=
      ( vx chil cfv cmul co wcel cno c1 cle wbr ax-mp cc0 wceq adantrr syl2an
      cr ccom wf cnop cxr cv wral cbo clo bdopln lnopcoi lnopfi nmopre remulcli
      wi rexr wa 0re leidi a1i ch0o lnopco0i nmlnop0iHIL sylib fveq1 fveq2d c0v
      ho0val norm0 syl6eq sylan9eq sylan opreq2 recni mul01i adantr 3brtr4d wne
      wn df-ne cdiv cabs recclzi ffvelrni norm-iii bdopf lnopmuli hocoi opreq2d
      csm cc adantl eqtr4d normcl syl recnd divrec2 mp3an2 ancoms rerecclzi clt
      wb nmopgt0 recgt0i sylbi ltle mpan absid syl2anc opreq1d 3eqtr4rd hvmulcl
      sylc 3eqtr4d nmoplb mp3an1 mulid2i biimpi 1re ledivmul2OLD mp3anl2 mpanl2
      syl6breqr mpbird eqbrtrd w3a 3jca ad2antrl mpbid sylanbr pm2.61ian nmopub
      ex rgen mp3an ) FFABUAZUBAUCGZBUCGZHIZUDJZEUEZKGLMNZYTYOGZKGZYRMNZUNZEFUF
      YOUCGZYRMNYOABAUGJZAUHJCAUIOZBUGJZBUHJDBUIOZUJZUKZYRTJYSYPYQUUGYPTJZCAULO
      ZUUIYQTJZDBULOZUMYRUOOUUEEFYTFJZUUAUUDYQPQZUUQUUAUPZUUDUURUUQUUDUUAUURUUQ
      UPZPPUUCYRMPPMNUUTPUQURUSUURYOUTQZUUQUUCPQUURUUFPQUVAABUUHUUJVAYOUUKVBVCU
      VAUUQUUCYTUTGZKGZPUVAUUBUVBKYTYOUTVDVEUUQUVCVFKGPUUQUVBVFKYTVGVEVHVIVJVKU
      URYRPQUUQUURYRYPPHIPYQPYPHVLYPYPUUNVMVNVIVOVPRUURVRYQPVQZUUSUUDYQPVSUVDUU
      SUPZUUCYQVTIZYPMNZUUDUVEUVFLYQVTIZYTBGZWIIZAGZKGZYPMUVDUUQUVFUVLQUUAUVDUU
      QUPZUVHUUBWIIZKGZUVHWAGZUUCHIZUVLUVFUVDUVHWJJZUUBFJZUVOUVQQUUQYQYQUUPVMZW
      BZFFYTYOUULWCZUVHUUBWDSUVMUVKUVNKUVMUVKUVHUVIAGZWIIZUVNUVDUVRUVIFJZUVKUWD
      QUUQUWAFFYTBUUIFFBUBZDBWEOZWCZUVHUVIAUUHWFSUUQUVNUWDQUVDUUQUUBUWCUVHWIYTA
      BUUGFFAUBZCAWEOZUWGWGWHWKWLVEUVMUVFUVHUUCHIZUVQUUQUVDUVFUWKQZUUQUUCWJJZUV
      DUWLUUQUUCUUQUVSUUCTJZUWBUUBWMWNZWOUWMYQWJJZUVDUWLUVTUUCYQWPWQVKWRUVMUVPU
      VHUUCHUVDUVPUVHQZUUQUVDUVHTJZPUVHMNZUWQYQUUPWSZUVDUWRPUVHWTNZUWSUWTUVDPYQ
      WTNZUXAUWFUVDUXBXAUWGBXBOZYQUUPXCXDPTJUWRUXAUWSUNUQPUVHXEXFXLUVHXGXHVOZXI
      WLXJRUVEUVJFJZUVJKGZLMNZUVLYPMNZUVDUUQUXEUUAUVDUVRUWEUXEUUQUWAUWHUVHUVIXK
      SRUVEUXFUVIKGZYQVTIZLMUVDUUQUXFUXJQUUAUVMUVPUXIHIZUVHUXIHIZUXFUXJUVMUVPUV
      HUXIHUXDXIUVDUVRUWEUXFUXKQUUQUWAUWHUVHUVIWDSUUQUVDUXJUXLQZUUQUXIWJJZUVDUX
      MUUQUXIUUQUWEUXITJZUWHUVIWMWNZWOUXNUWPUVDUXMUVTUXIYQWPWQVKWRXMRUVEUXJLMNZ
      UXILYQHIZMNZUUSUXSUVDUUSUXIYQUXRMUWFUUQUUAUXIYQMNUWGYTBXNXOYQUVTXPYBWKUVD
      UUQUXQUXSXAZUUAUUQUVDUXTUUQUXOUXBUXTUVDUXPUVDUXBUXCXQZUXOLTJZUXBUXTXRUXOU
      UOUYBUXBUXTUUPUXIYQLXSXTYASWRRYCYDUWIUXEUXGUXHUWJUVJAXNXOXHYDUVEUWNUUOUUM
      YEZUXBUVGUUDXAUUQUYCUVDUUAUUQUWNUUOUUMUWOUUOUUQUUPUSUUMUUQUUNUSYFYGUVDUXB
      UUSUYAVOUUCYQYPXSXHYHYIYJYLYMEYRYOYKYN $.
      $( [10-Mar-2006] $)

    $( The sum of two bounded linear operators is a bounded linear operator. $)
    bdophsi $p |- ( S +op T ) e. BndLinOp $=
      ( chos co cbo wcel clo cnop cfv cr elbdop2 bdopln ax-mp wbr chil wf bdopf
      nmopre lnophsi cxr caddc cmnf hoaddcli nmopxr readdcli nmopgtmnf nmoptrii
      clt cle xrre mp4an mpbir2an ) ABEFZGHUOIHUOJKZLHZUOMABAGHZAIHCANOBGHZBIHD
      BNOUAUPUBHZAJKZBJKZUCFZLHUDUPUJPZUPVCUKPUQQQUORZUTABURQQARCASOUSQQBRDBSOU
      EZUOUFOVAVBURVALHCATOUSVBLHDBTOUGVEVDVFUOUHOABCDUIUPVCULUMUN $.
      $( [9-Mar-2006] $)

    $( The difference between two bounded linear operators is bounded. $)
    bdophdi $p |- ( S -op T ) e. BndLinOp $=
      ( c1 cneg chot co chos chod cbo wcel chil wf bdopf ax-mp honegsubi ax-1cn
      cc negcli bdophmi bdophsi eqeltrri ) AEFZBGHZIHABJHKABAKLMMANCAOPBKLMMBND
      BOPQAUECUDSLUEKLERTUDBDUAPUBUC $.
      $( [10-Mar-2006] $)

    $( The composition of two bounded linear operators is bounded. $)
    bdopcoi $p |- ( S o. T ) e. BndLinOp $=
      ( ccom cbo wcel clo cnop cfv cr elbdop2 bdopln ax-mp lnopcoi cxr wbr chil
      lnopfi nmopre cmul co clt cle wf hocofi nmopxr remulcli nmopgtmnf nmopcoi
      cmnf xrre mp4an mpbir2an ) ABEZFGUOHGUOIJZKGZUOLABAFGZAHGCAMNZBFGZBHGDBMN
      ZOUPPGZAIJZBIJZUAUBZKGUKUPUCQZUPVEUDQUQRRUOUEZVBABAUSSBVASUFZUOUGNVCVDURV
      CKGCATNUTVDKGDBTNUHVGVFVHUOUINABCDUJUPVEULUMUN $.
      $( [9-Mar-2006] $)

    $( Triangle-type inequality for the norms of bounded linear operators. $)
    nmoptri2i $p |- ( ( normop ` S ) - ( normop ` T ) ) <_
                  ( normop ` ( S +op T ) ) $=
      ( cnop cfv co chos cle wbr caddc c1 wcel cbo ax-mp chil wf bdopf nmopre
      cr cmin cneg chot bdophsi cc ax-1cn negcli bdophmi nmoptrii chod hoaddcli
      honegsubi hopncani eqtri fveq2i nmopnegi opreq2i 3brtr3i lesubaddi mpbir
      ) AEFZBEFZUAGABHGZEFZIJVAVDVBKGZIJVCLUBZBUCGZHGZEFVDVGEFZKGVAVEIVCVGABCDU
      DZVFUEMVGNMLUFUGVFBDUHOUIVHAEVHVCBUJGAVCBABANMZPPAQCAROZBNMZPPBQDBROZUKVN
      ULABVLVNUMUNUOVIVBVDKBVNUPUQURVAVBVDVKVATMCASOVMVBTMDBSOVCNMVDTMVJVCSOUSU
      T $.
      $( [26-Nov-2006] $) $( [10-Mar-2006] $)

    $( The adjoint of a composition of bounded linear operators.  Theorem
       3.11(viii) of [Beran] p. 106. $)
    adjcoi $p |- ( adjh ` ( S o. T ) ) = ( ( adjh ` T ) o. ( adjh ` S ) ) $=
      ( vx vy cado cfv csp co wceq chil wcel cbo wf adjbdln ax-mp bdopf bdopadj
      adj2 cv ccom wral hocoi opreq2d adantl opreq1d adantr ffvelrni cdm mp3an1
      wa sylan sylan2 3eqtrd bdopcoi 3eqtr2rd rgen2a wb hocofi hoeq2 mp2an mpbi
      ) EUAZFUAZABUBZGHZHIJZVDVEBGHZAGHZUBZHZIJZKZFLUCELUCZVGVKKZVNEFLVDLMZVELM
      ZULZVMVDVEVJHZVIHZIJZVDVFHZVEIJZVHVRVMWBKVQVRVLWAVDIVEVIVJVINMZLLVIOBNMZW
      EDBPQVIRQZVJNMZLLVJOANMZWHCAPQVJRQZUDUEUFVSWDVDBHZAHZVEIJZWKVTIJZWBVQWDWM
      KVRVQWCWLVEIVDABWILLAOCARQWFLLBODBRQZUDUGUHVQWKLMZVRWMWNKZLLVDBWOUIAGUJZM
      ZWPVRWQWIWSCASQWKVEATUKUMVRVQVTLMZWNWBKZLLVEVJWJUIBWRMZVQWTXAWFXBDBSQVDVT
      BTUKUNUOVFWRMZVQVRWDVHKVFNMZXCABCDUPZVFSQVDVEVFTUKUQURLLVGOZLLVKOVOVPUSVG
      NMZXFXDXGXEVFPQVGRQVIVJWGWJUTEFVGVKVAVBVC $.
      $( [10-Mar-2006] $)
  $}

  ${
    $d x y S $.  $d x y T $.
    nmopcoadj.1 $e |- T e. BndLinOp $.
    $( The norm of an operator composed with its adjoint.  Part of Theorem
       3.11(vi) of [Beran] p. 106. $)
    nmopcoadji $p |- ( normop ` ( ( adjh ` T ) o. T ) )
                    = ( ( normop ` T ) ^ 2 ) $=
      ( vx cfv co wceq cle wbr wcel cr ax-mp chil cno c1 wa cmul adantr syl cc0
      mp2b cado ccom cnop cexp cbo adjbdlnb mpbi bdopcoi nmopre resqcli letri3i
      c2 wf cxr cv wi wral bdopf hocofi rexr hocoi fveq2d ffvelrni 3syl remulcl
      normcl sylancr remulcli a1i nmbdoplbi nmopge0 pm3.2i lemul2a mpanl2 sylan
      1re mp3anl3 recni mulid1i syl6breq letrd syl21anc eqbrtrd nmopadji sqvali
      opreq1i eqtr4i ex rgen nmopub mp3an csqr sqrcli csp cabs cc mpancom abscl
      syl2anc bcs hococli normge0 jca simpr mp3anl2 recnd mulid1 eqtr4d breqtrd
      hicl resqcl sqge0 absid normsq bdopadj adj2 mp3an1 adjadj opreq2i syl6req
      fveq1i eqtrd eqtr3d sqsqri 3brtr4d wb sqrge0i le2sq mpanr12 mpbird le2sqi
      cdm mp2an breqtri mpbir2an ) AUADZAUBZUCDZAUCDZULUDEZFYRYTGHZYTYRGHYRYTYQ
      UEIYRJIZYPAAUEIZYPUEIZBAUFUGZBUHZYQUIKZYSUUCYSJIZBAUIKZUJZUKLLYQUMZYTUNIZ
      CUOZMDZNGHZUUMYQDZMDZYTGHZUPZCLUQUUAYPAUUDLLYPUMZUUEYPURZKZUUCLLAUMZBAURZ
      KZUSZYTJIUULUUJYTUTKUUSCLUUMLIZUUOUURUVGUUOOZUUQYPUCDZYSPEZYTGUVHUUQUUMAD
      ZYPDZMDZUVJGUVGUUQUVMFUUOUVGUUPUVLMUUMYPAUVBUVEVAVBZQUVHUVMUVIUVKMDZPEZUV
      JUVGUVMJIZUUOUVGUVKLIZUVLLIZUVQLLUUMAUVEVCZLLUVKYPUVBVCZUVLVFVDZQUVGUVPJI
      ZUUOUVGUVIJIZUVOJIZUWCUUDUWDUUEYPUIKZUVGUVRUWEUVTUVKVFZRZUVIUVOVEVGQUVJJI
      UVHUVIYSUWFUUIVHVIUVGUVMUVPGHZUUOUVGUVRUWIUVTUVKYPUUEVJRQUVHUWEUUHUVOYSGH
      ZUVPUVJGHZUVGUWEUUOUWHQZUUHUVHUUIVIZUVHUVOYSUUNPEZYSUWLUVGUWNJIZUUOUVGUUH
      UUNJIZUWOUUIUUMVFZYSUUNVEVGQUWMUVGUVOUWNGHUUOUUMABVJQUVHUWNYSNPEZYSGUVGUW
      PUUOUWNUWRGHZUWQUWPNJIZUUOUWSVPUWPUWTUUHSYSGHZOUUOUWSUUHUXAUUIUUCUVCUXABU
      VDAVKTZVLUUNNYSVMVQVNVOYSYSUUIVRZVSVTWAUWEUUHUWDSUVIGHZOUWJUWKUWDUXDUWFUU
      DUUTUXDUUEUVAYPVKTVLUVOYSUVIVMVQWBWAWCUVJYSYSPEYTUVIYSYSPABWDWFYSUXCWEWGV
      TWHWICYTYQWJWKYTYRWLDZULUDEZYRGYSUXEGHZYTUXFGHZUVCUXEUNIZUUOUVOUXEGHZUPZC
      LUQUXGUVESYRGHZUXEJIZUXIUUKUXLUVFYQVKZKZYRUUGWMZUXEUTTUXKCLUVGUUOUXJUVHUX
      JUVOULUDEZUXFGHZUVHUVLUUMWNEZWODZYRUXQUXFGUVHUXTUVMUUNPEZYRUVGUXTJIZUUOUV
      GUXSWPIZUYBUVSUVGUYCUVGUVRUVSUVTUWARZUVLUUMXJWQUXSWRRQUVGUYAJIZUUOUVGUVQU
      WPUYEUWBUWQUVMUUNVEWSQZUUBUVHUUGVIZUVGUXTUYAGHZUUOUVSUVGUYHUYDUVLUUMWTWQQ
      UVHUYAUUQYRUYFUVGUUQJIZUUOUVGUUPLIUYIUUMYPAUVBUVEXAUUPVFRQZUYGUVHUYAUVMNP
      EZUUQGUVHUWPUVQSUVMGHZOZUUOUYAUYKGHZUVGUWPUUOUWQQUVGUYMUUOUVGUVQUYLUWBUVG
      UVRUVSUYLUVTUWAUVLXBVDXCQUVGUUOXDUWPUWTUYMUUOUYNVPUUNNUVMVMXEWBUVGUYKUUQF
      UUOUVGUYKUVMUUQUVGUVMWPIUYKUVMFUVGUVMUWBXFUVMXGRUVNXHQXIUVHUUQYRUUNPEZYRU
      YJUVGUYOJIZUUOUVGUUBUWPUYPUUGUWQYRUUNVEVGQUYGUVGUUQUYOGHUUOUUMYQUUFVJQUVH
      UYOYRNPEZYRGUVGUWPUUOUYOUYQGHZUWQUWPUWTUUOUYRVPUWPUWTUUBUXLOUUOUYRUUBUXLU
      UGUXOVLUUNNYRVMVQVNVOYRYRUUGVRVSVTWAWAWAUVGUXQUXTFUUOUVGUXQWODZUXQUXTUVGU
      VRUWEUYSUXQFZUVTUWGUWEUXQJISUXQGHUYTUVOXKUVOXLUXQXMWSVDUVGUXQUXSWOUVGUXQU
      VKUVKWNEZUXSUVGUVRUXQVUAFUVTUVKXNRUVGUXSUVKUUMYPUADZDZWNEZVUAUVRUVGUXSVUD
      FZUVTYPUAYLZIZUVRUVGVUEUUDVUGUUEYPXOKUVKUUMYPXPXQWQVUCUVKUVKWNUUMVUBAUUCA
      VUFIVUBAFBAXOAXRTYAXSXTYBVBYCQUXFYRFZUVHUUKUXLVUHUVFUXNYRUUGYDTZVIYEUVGUX
      JUXRYFZUUOUVGUWESUVOGHZVUJUWHUVGUVRVUKUVTUVKXBRUWEVUKOUXMSUXEGHZVUJUUKUXL
      UXMUVFUXNUXPTZUUKUXLVULUVFUXNYRUUGYGTZUVOUXEYHYIWSQYJWHWICUXEAWJWKUXAVULU
      XGUXHYFUXBVUNYSUXEUUIVUMYKYMUGVUIYNYO $.
      $( [8-Mar-2006] $)

    $( The norm of an operator composed with its adjoint.  Part of Theorem
       3.11(vi) of [Beran] p. 106. $)
    nmopcoadj2i $p |- ( normop ` ( T o. ( adjh ` T ) ) )
                    = ( ( normop ` T ) ^ 2 ) $=
      ( cado cfv ccom cnop c2 cexp co cbo wcel adjbdln ax-mp nmopcoadji bdopadj
      cdm wceq adjadj coeq1i fveq2i nmopadji opreq1i 3eqtr3i ) ACDZCDZUDEZFDUDF
      DZGHIAUDEZFDAFDZGHIUDAJKZUDJKBALMNUFUHFUEAUDACPKZUEAQUJUKBAOMARMSTUGUIGHA
      BUAUBUC $.
      $( [10-Mar-2006] $)

    $( An operator composed with its adjoint is zero iff the operator is zero.
       Theorem 3.11(vii) of [Beran] p. 106. $)
    nmopcoadj0i $p |- ( ( T o. ( adjh ` T ) ) = 0hop <-> T = 0hop ) $=
      ( cado cfv ccom cnop cc0 wceq ch0o c2 cexp co nmopcoadj2i eqeq1i cbo wcel
      ax-mp clo bdopln nmlnop0iHIL cr nmopre recni sqeq0i bitri adjbdln lnopcoi
      3bitr3i ) AACDZEZFDZGHZAFDZGHZUJIHAIHULUMJKLZGHUNUKUOGABMNUMUMAOPZUMUAPBA
      UBQUCUDUEUJAUIUPARPBASQZUIOPZUIRPUPURBAUFQUISQUGTAUQTUH $.
      $( [2-Jul-2008] $) $( [10-Mar-2006] $)
  $}

