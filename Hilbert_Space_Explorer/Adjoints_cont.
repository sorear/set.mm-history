$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Adjoints (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g h u v w y z A $.  $d u B $.  $d f C $.  $d f t w x z F $.
    $d f v w x z G $.  $d f g h t u v w x y z T $.
    cnlnadjlem.1 $e |- T e. LinOp $.
    cnlnadjlem.2 $e |- T e. ConOp $.
    cnlnadjlem.3 $e |- G =
                    { <. g , h >. | ( g e. ~H /\ h = ( ( T ` g ) .ih y ) ) } $.
    $( Lemma for ~ cnlnadji (Theorem 3.10 of [Beran] p. 104: every continuous
       linear operator has an adjoint).  The value of the auxiliary functional
       ` G ` . $)
    cnlnadjlem1 $p |- ( A e. ~H -> ( G ` A ) = ( ( T ` A ) .ih y ) ) $=
      ( cv cfv csp co chil wceq fveq2 opreq1d oprex fvopab4 ) DEBDJZCKZAJZLMBCK
      ZUBLMNFTBOUAUCUBLTBCPQIUCUBLRS $.
      $( [16-Feb-2006] $)

    $( Lemma for ~ cnlnadji . ` G ` is a continuous linear functional. $)
    cnlnadjlem2 $p |- ( y e. ~H -> ( G e. LinFn /\ G e. ConFn ) ) $=
      ( vx vw vz cv chil wcel clf ccnf cc wf csm co cva cfv cmul caddc wceq
      wral csp lnopfi ffvelrni hicl sylan ancoms r19.21aiva fopab2 sylib wa
      hvmulcl w3a lnopaddi 3adant3 opreq1d id ax-his2 syl3an eqtrd 3comr 3expa
      sylanl2 hvaddcl cnlnadjlem1 syl adantll ax-his3 syl3an2 3expb lnopmuli
      adantl opreq2d ad2antll 3eqtr4rd opreqan12d 3eqtr4d r19.21aivva ellnfn
      sylanbrc cabs cno cle wbr cr wrex cnop nmcopexi normcl remulcl sylancr
      adantr eqeltrd abscl syl2an fveq2d bcs eqbrtrd cc0 normge0 jca nmcoplbi
      lemul1a syl31anc letrd recnd recni mul32 mp3an1 breqtrd opreq1 breq2d
      ralbidv rcla4ev syl11anc wb lnfncon mpbird ) ALZMNZEONZEPNZYEMQERZILZJLZS
      TZKLZUATZEUBZYIYJEUBZUCTZYLEUBZUDTZUEZKMUFZJMUFIQUFYFYECLZBUBZYDUGTZQNZCM
      UFYHYEUUDCMUUAMNZYEUUDUUEUUBMNYEUUDMMUUABBFUHZUIUUBYDUJUKULUMCDMQUUCEHUNU
      OYEYTIJQMYEYIQNZYJMNZUPZUPZYSKMUUJYLMNZUPYMBUBZYDUGTZYKBUBZYDUGTZYLBUBZYD
      UGTZUDTZYNYRUUIYEYKMNZUUKUUMUURUEZYIYJUQZYEUUSUUKUUTUUSUUKYEUUTUUSUUKYEUR
      ZUUMUUNUUPUATZYDUGTZUURUVBUULUVCYDUGUUSUUKUULUVCUEYEYKYLBFUSUTVAUUSUUNMNU
      UKUUPMNZYEYEUVDUURUEMMYKBUUFUIMMYLBUUFUIZYEVBUUNUUPYDVCVDVEVFVGVHUUIUUKYN
      UUMUEZYEUUIUUKUPYMMNZUVGUUIUUSUUKUVHUVAYKYLVIUKAYMBCDEFGHVJVKVLUUJUUKYPUU
      OYQUUQUDUUJYIYJBUBZSTZYDUGTZYIUVIYDUGTZUCTZUUOYPYEUUGUUHUVKUVMUEZUUGUUHYE
      UVNUUHUUGUVIMNYEUVNMMYJBUUFUIYIUVIYDVMVNVFVOUUIUUOUVKUEYEUUIUUNUVJYDUGYIY
      JBFVPVAVQUUHYPUVMUEYEUUGUUHYOUVLYIUCAYJBCDEFGHVJVRVSVTAYLBCDEFGHVJZWAWBUM
      WCIJKEWDWEZYEYGYQWFUBZYIYLWGUBZUCTZWHWIZKMUFZIWJWKZYEBWLUBZYDWGUBZUCTZWJN
      ZUVQUWEUVRUCTZWHWIZKMUFZUWBYEUWCWJNZUWDWJNZUWFBFGWMZYDWNZUWCUWDWOWPYEUWHK
      MUUKYEUWHUUKYEUPZUVQUWCUVRUCTZUWDUCTZUWGWHUWNUVQUUPWGUBZUWDUCTZUWPUWNYQQN
      UVQWJNUWNYQUUQQUUKYQUUQUEYEUVOWQZUUKUVEYEUUQQNUVFUUPYDUJUKWRYQWSVKUUKUWQW
      JNZUWKUWRWJNYEUUKUVEUWTUVFUUPWNVKZUWMUWQUWDWOWTUUKUWOWJNZUWKUWPWJNYEUUKUW
      JUVRWJNUXBUWLYLWNZUWCUVRWOWPZUWMUWOUWDWOWTUWNUVQUUQWFUBZUWRWHUWNYQUUQWFUW
      SXAUUKUVEYEUXEUWRWHWIUVFUUPYDXBUKXCUWNUWTUXBUWKXDUWDWHWIZUPZUWQUWOWHWIZUW
      RUWPWHWIUUKUWTYEUXAWQUUKUXBYEUXDWQYEUXGUUKYEUWKUXFUWMYDXEXFVQUUKUXHYEYLBF
      GXGWQUWQUWOUWDXHXIXJUUKUVRQNZUWDQNZUWPUWGUEZYEUUKUVRUXCXKYEUWDUWMXKUWCQNU
      XIUXJUXKUWCUWLXLUWCUVRUWDXMXNWTXOULUMUWAUWIIUWEWJYIUWEUEZUVTUWHKMUXLUVSUW
      GUVQWHYIUWEUVRUCXPXQXRXSXTYEYFYGUWBYAUVPIKEYBVKYCXF $.
      $( [16-Feb-2006] $)

    ${
      cnlnadjlem.4 $e |- B = U. { w e. ~H | A. v e. ~H ( ( T ` v ) .ih y )
                         = ( v .ih w ) } $.
      cnlnadjlem.5 $e |- F = { <. y , u >. | ( y e. ~H /\ u = B ) } $.
      $( Lemma for ~ cnlnadji .  By ~ riesz4 , ` B ` is the unique vector such
         that ` ( T `` v ) .ih y ) = ( v .ih w ) ` for all ` v ` . $)
      cnlnadjlem3 $p |- ( y e. ~H -> B e. ~H ) $=
        ( cv chil wcel cfv csp co wceq wral crab cuni wreu clf ccnf cin wa
        cnlnadjlem2 elin sylibr riesz4 syl cnlnadjlem1 eqeq1d ralbiia reubii
        sylib reucl syl5eqel ) APZQRZECPZFSVCTUAZVEBPTUAZUBZCQUCZBQUDUEZQNVDVIB
        QUFZVJQRVDVEJSZVGUBZCQUCZBQUFZVKVDJUGUHUIRZVOVDJUGRJUHRUJVPAFGHJKLMUKJU
        GUHULUMBCJUNUOVNVIBQVMVHCQVEQRVLVFVGAVEFGHJKLMUPUQURUSUTVIBQVAUOVB $.
        $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadji .  The values of auxiliary function ` F ` are
         vectors. $)
      cnlnadjlem4 $p |- ( A e. ~H -> ( F ` A ) e. ~H ) $=
        ( chil wcel cfv cv csp co wceq wral crab cuni opreq2 eqeq1d ralbidv
        rabbidv unieqd syl5eq ax-hilex rabex uniex fvopab4 wreu reubidv clf
        ccnf cin wa cnlnadjlem2 elin sylibr riesz4 syl cnlnadjlem1 ralbiia
        reubii sylib vtoclga reucl eqeltrd ) EQRZEJSCTZGSZEUAUBZVPBTUAUBZUCZCQU
        DZBQUEZUFZQADEFWCQJATZEUCZFVQWDUAUBZVSUCZCQUDZBQUEZUFWCOWEWIWBWEWHWABQW
        EWGVTCQWEWFVRVSWDEVQUAUGUHUIZUJUKULPWBWABQUMUNUOUPVOWABQUQZWCQRWHBQUQZW
        KAEQWEWHWABQWJURWDQRZVPKSZVSUCZCQUDZBQUQZWLWMKUSUTVARZWQWMKUSRKUTRVBWRA
        GHIKLMNVCKUSUTVDVEBCKVFVGWPWHBQWOWGCQVPQRWNWFVSAVPGHIKLMNVHUHVIVJVKVLWA
        BQVMVGVN $.
        $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadji . ` F ` is an adjoint of ` T ` (later, we will
         show it is unique). $)
      cnlnadjlem5 $p |- ( ( A e. ~H /\ C e. ~H ) -> ( ( T ` C ) .ih A ) =
                        ( C .ih ( F ` A ) ) ) $=
        ( vf vz chil wcel cfv csp co wceq cv wi fveq2 opreq1d opreq1 eqeq12d
        imbi2d wral ax-17 wa copab hbopab1 hbxfr hbfv hbopr hbeq hbral opreq2
        opreq2d ralbidv crab cuni fveq1i cvv ax-hilex rabex uniex eqeltri
        fvopab2 mpan2 syl5eq wb cbvralv a1i rabbiia unieqi eqtri syl6req wreu
        cnlnadjlem3 eqeltrd clf ccnf cin cnlnadjlem2 elin sylibr riesz4 syl
        cnlnadjlem1 eqeq1d ralbiia reubii sylib eqeq2d reuuni2 syl11anc mpbird
        vtoclgaf ra4 com12 vtoclga impcom ) GTUAETUAZGHUBZEUCUDZGEKUBZUCUDZUEZX
        IRUFZHUBZEUCUDZXOXLUCUDZUEZUGXIXNUGRGTXOGUEZXSXNXIXTXQXKXRXMXTXPXJEUCXO
        GHUHUIXOGXLUCUJUKULXIXOTUAZXSXIXSRTUMZYAXSUGXPAUFZUCUDZXOYCKUBZUCUDZUEZ
        RTUMZYBASETSUFZEUAAUNZXSARTYAAUNASSXQXRYIXQUAAUNASXOXLUCYIXOUAAUNYIUCUA
        AUNASEKASKYCTUAZDUFFUEUOZADUPZQYLADSUQURYJUSUTVAVBYCEUEZYGXSRTYNYDXQYFX
        RYCEXPUCVCYNYEXLXOUCYCEKUHVDUKVEYKYHYDXOBUFZUCUDZUEZRTUMZBTVFZVGZYEUEZY
        KYEFYTYKYEYCYMUBZFYCKYMQVHYKFVIUAUUBFUEFCUFZHUBZYCUCUDZUUCYOUCUDZUEZCTU
        MZBTVFZVGZVIPUUIUUHBTVJVKVLVMADTFVIVNVOVPZFUUJYTPUUIYSUUHYRBTUUHYRVQYOT
        UAUUGYQCRTUUCXOUEZUUEYDUUFYPUULUUDXPYCUCUUCXOHUHUIUUCXOYOUCUJUKVRVSVTWA
        WBWCYKYETUAYRBTWDZYHUUAVQYKYEFTUUKABCDFHIJKLMNOPQWEWFYKXOLUBZYPUEZRTUMZ
        BTWDZUUMYKLWGWHWIUAZUUQYKLWGUALWHUAUOUURAHIJLMNOWJLWGWHWKWLBRLWMWNUUPYR
        BTUUOYQRTYAUUNYDYPAXOHIJLMNOWOWPWQWRWSYRYHBTYEYOYEUEZYQYGRTUUSYPYFYDYOY
        EXOUCVCWTVEXAXBXCXDXSRTXEWNXFXGXH $.
        $( [18-Feb-2006] $)

      $( Lemma for ~ cnlnadji . ` F ` is linear. $)
      cnlnadjlem6 $p |- F e. LinOp $=
        ( vx vf vz vt clo wcel chil wf cv csm co cva cfv wceq wral cc ellnop
        cnlnadjlem3 fopab wa csp caddc lnopfi ffvelrni adantl hvmulcl ad2antrr
        simplr his7 syl111anc hvaddcl sylan cnlnadjlem5 ccj cmul adantll
        opreq2d simpll his5 simpr cnlnadjlem4 ad2antlr 3eqtr4d adantlr
        opreq12d sylan2 eqtr4d 3eqtr3d r19.21aiva wb syl syl2an hial2eq2
        syl11anc mpbid rgen2 mpbir2an ) ITUAUBUBIUCPUDZQUDZUEUFZRUDZUGUFZIUHZWM
        WNIUHZUEUFZWPIUHZUGUFZUIZRUBUJZQUBUJPUKUJPQRIULADUBUBEIOABCDEFGHIJKLMNO
        UMUNXDPQUKUBWMUKUAZWNUBUAZUOZXCRUBXGWPUBUAZUOZSUDZWRUPUFZXJXBUPUFZUIZSU
        BUJZXCXIXMSUBXIXJUBUAZUOZXJFUHZWQUPUFZXQWOUPUFZXQWPUPUFZUQUFZXKXLXPXQUB
        UAZWOUBUAZXHXRYAUIXOYBXIUBUBXJFFKURUSZUTXGYCXHXOWMWNVAZVBXGXHXOVCXQWOWP
        VDVEXIWQUBUAZXOXRXKUIXGYCXHYFYEWOWPVFVGZABCDWQEXJFGHIJKLMNOVHVGXPYAXJWT
        UPUFZXJXAUPUFZUQUFZXLXPXSYHXTYIUQXGXOXSYHUIXHXGXOUOZWMVIUHZXQWNUPUFZVJU
        FZYLXJWSUPUFZVJUFZXSYHYKYMYOYLVJXFXOYMYOUIXEABCDWNEXJFGHIJKLMNOVHVKVLYK
        XEYBXFXSYNUIXEXFXOVMZXOYBXGYDUTXEXFXOVCWMXQWNVNVEYKXEXOWSUBUAZYHYPUIYQX
        GXOVOXFYRXEXOABCDWNEFGHIJKLMNOVPZVQWMXJWSVNVEVRVSXHXOXTYIUIXGABCDWPEXJF
        GHIJKLMNOVHVKVTXPXOWTUBUAZXAUBUAZXLYJUIXIXOVOXGYTXHXOXFXEYRYTYSWMWSVAWA
        ZVBXHUUAXGXOABCDWPEFGHIJKLMNOVPZVQXJWTXAVDVEWBWCWDXIWRUBUAZXBUBUAZXNXCW
        EXIYFUUDYGABCDWQEFGHIJKLMNOVPWFXGYTUUAUUEXHUUBUUCWTXAVFWGSWRXBWHWIWJWDW
        KWL $.
        $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadji .  Helper lemma to show that ` F ` is
         continuous. $)
      cnlnadjlem7 $p |- ( A e. ~H -> ( normh ` ( F ` A ) ) <_
                        ( ( normop ` T ) x. ( normh ` A ) ) ) $=
        ( chil wcel cfv cno cnop cmul co cle wbr cc0 breq1 wne wa csp cabs cc
        cr cnlnadjlem4 lnopfi ffvelrni syl hicl mpancom abscl normcl remulcl
        syl11anc nmcopexi sylancr bcs normge0 jca nmcoplbi lemul1a syl31anc
        letrd c2 cexp wceq normsq recnd sqval cnlnadjlem5 mpdan fveq2d hiidrcl
        hiidge0 absid eqtr2d 3eqtr3rd recni mul32 mp3an1 3brtr3d adantr clt wb
        0re leltne biimpar sylan lemul1 syl112anc mpbird wf nmopge0 ax-mp
        mulge0 mpanl12 pm2.61ne ) EQRZEJSZTSZGUASZETSZUBUCZUDUEZUFXLUDUEZXIUFXI
        UFXLUDUGXGXIUFUHZUIZXMXIXIUBUCZXLXIUBUCZUDUEZXGXSXOXGXHGSZEUJUCZUKSZXJX
        IUBUCZXKUBUCZXQXRUDXGYBXTTSZXKUBUCZYDXGYAULRZYBUMRXTQRZXGYGXGXHQRZYHABC
        DEFGHIJKLMNOPUNZQQXHGGLUOZUPUQZXTEURUSYAUTUQXGYEUMRZXKUMRZYFUMRXGYHYMYL
        XTVAUQZEVAZYEXKVBVCXGYCUMRZYNYDUMRXGXJUMRZXIUMRZYQGLMVDZXGYIYSYJXHVAZUQ
        ZXJXIVBVEZYPYCXKVBVCYHXGYBYFUDUEYLXTEVFUSXGYMYQYNUFXKUDUEZUIZYEYCUDUEZY
        FYDUDUEYOUUCXGYNUUDYPEVGZVHXGYIUUFYJXHGLMVIUQYEYCXKVJVKVLXGXIVMVNUCZXHX
        HUJUCZXQYBXGYIUUHUUIVOYJXHVPUQXGXIULRZUUHXQVOXGXIUUBVQZXIVRUQXGYBUUIUKS
        ZUUIXGYAUUIUKXGYIYAUUIVOYJABCDEFXHGHIJKLMNOPVSVTWAXGUUIUMRZUFUUIUDUEZUU
        LUUIVOXGYIUUMYJXHWBUQXGYIUUNYJXHWCUQUUIWDVCWEWFXGUUJXKULRZYDXRVOZUUKXGX
        KYPVQXJULRUUJUUOUUPXJYTWGXJXIXKWHWIVCWJWKXPYSXLUMRZYSUFXIWLUEZXMXSWMXGY
        SXOUUBWKZXGUUQXOXGYRYNUUQYTYPXJXKVBVEWKUUSXGYIXOUURYJYIUURXOYIYSUFXIUDU
        EZUURXOWMZUUAXHVGUFUMRYSUUTUVAWNUFXIWOWIVCWPWQXIXLXIWRWSWTXGYNUUDXNYPUU
        GYRUFXJUDUEZUUEXNYTQQGXAUVBYKGXBXCXJXKXDXEVCXF $.
        $( [18-Feb-2006] $)

      $( Lemma for ~ cnlnadji . ` F ` is continuous. $)
      cnlnadjlem8 $p |- F e. ConOp $=
        ( vz vx cco wcel cv cfv cno cmul co cle wbr chil wral cr wrex cnop
        nmcopexi cnlnadjlem7 rgen wceq opreq1 breq2d ralbidv rcla4ev mp2an
        cnlnadjlem6 lnopconi mpbir ) IRSPTZIUAUBUAZQTZVDUBUAZUCUDZUEUFZPUGUHZQU
        IUJZFUKUAZUISVEVLVGUCUDZUEUFZPUGUHZVKFKLULVNPUGABCDVDEFGHIJKLMNOUMUNVJV
        OQVLUIVFVLUOZVIVNPUGVPVHVMVEUEVFVLVGUCUPUQURUSUTQPIABCDEFGHIJKLMNOVAVBV
        C $.
        $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadji . ` F ` provides an example showing the existence
         of a continuous linear adjoint. $)
      cnlnadjlem9 $p |- E. t e. ( LinOp i^i ConOp )
             A. x e. ~H A. z e. ~H ( ( T ` x ) .ih z ) = ( x .ih ( t ` z ) ) $=
        ( clo cco cin wcel cv cfv csp co wceq chil wral wrex elin cnlnadjlem6
        cnlnadjlem8 mpbir2an cnlnadjlem5 ancoms rgen2a fveq1 opreq2d eqeq2d
        2ralbidv rcla4ev mp2an ) LSTUAZUBZAUCZIUDCUCZUEUFZVFVGLUDZUEUFZUGZCUHUI
        AUHUIZVHVFVGGUCZUDZUEUFZUGZCUHUIAUHUIZGVDUJVELSUBLTUBLSTUKBDEFHIJKLMNOP
        QRULBDEFHIJKLMNOPQRUMUNVKACUHVGUHUBVFUHUBVKBDEFVGHVFIJKLMNOPQRUOUPUQVQV
        LGLVDVMLUGZVPVKACUHUHVRVOVJVHVRVNVIVFUEVGVMLURUSUTVAVBVC $.
        $( [18-Feb-2006] $)
    $}
  $}

  ${
    $d f g h t u v w x y z T $.
    cnlnadj.1 $e |- T e. LinOp $.
    cnlnadj.2 $e |- T e. ConOp $.
    $( Every continuous linear operator has an adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadji $p |- E. t e. ( LinOp i^i ConOp )
             A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) $=
      ( vz vw vv vu vf vg vh cv cfv csp co wceq chil wral crab cuni wcel wa
      copab eqid opreq2 eqeq2d ralbidv cbvrabv unieqi cnlnadjlem9 ) AGBHIJCINZD
      OGNZPQZUMKNZPQZRZISTZKSUAZUBZDLMUNSUCJNVARUDGJUEZLNZSUCMNVCDOUNPQRUDLMUEZ
      EFVDUFUTUOUMHNZPQZRZISTZHSUAUSVHKHSUPVERZURVGISVIUQVFUOUPVEUMPUGUHUIUJUKV
      BUFUL $.
      $( [18-Feb-2006] $)

    $( Every continuous linear operator has a unique adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadjeui $p |- E! t e. ( LinOp i^i ConOp )
             A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) $=
      ( cv cfv csp co wceq chil wral clo cco cin wreu wrex wcel wa wmo reu5
      cnlnadji wf adjmo inss1 sseli lnopf syl simpl eqcom 2ralbii wb lnopfi
      adjsym mpan2 syl5bb biimpa jca sylan immoi ax-mp mpbir2an ) AGZDHBGZIJZVD
      VECGZHIJZKZBLMALMZCNOPZQVJCVKRVGVKSZVJTZCUAZVJCVKUBABCDEFUCLLVGUDZVDVEDHI
      JVDVGHVEIJKBLMALMZTZCUAVNABCDUEVMVQCVLVOVJVQVLVGNSVOVKNVGNOUFUGVGUHUIVOVJ
      TVOVPVOVJUJVOVJVPVJVHVFKZBLMALMZVOVPVIVRABLLVFVHUKULVOLLDUDVSVPUMDEUNABVG
      DUOUPUQURUSUTVAVBVC $.
      $( [18-Feb-2006] $)
  $}

  ${
    $d t x y T $.
    $( Every continuous linear operator has a unique adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadjeu $p |- ( T e. ( LinOp i^i ConOp ) -> E! t e. ( LinOp i^i ConOp )
           A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) ) $=
      ( clo cco cin wcel cv cfv csp co wceq chil wral wreu ch0o cif fveq1
      opreq1d eqeq1d 2ralbidv reubidv inss1 elin 0lnop 0cnop mpbir2an elimel
      sselii inss2 cnlnadjeui dedth ) DEFGZHZAIZDJZBIZKLZUPURCIJKLZMZBNOANOZCUN
      PUPUODQRZJZURKLZUTMZBNOANOZCUNPDQDVCMZVBVGCUNVHVAVFABNNVHUSVEUTVHUQVDURKU
      PDVCSTUAUBUCABCVCUNEVCEFUDDQUNQUNHQEHQFHQEFUEUFUGUHUIZUJUNFVCEFUKVIUJULUM
      $.
      $( [19-Feb-2006] $)

    $( Every continuous linear operator has an adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadj $p |- ( T e. ( LinOp i^i ConOp ) -> E. t e. ( LinOp i^i ConOp )
           A. x e. ~H A. y e. ~H ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) ) $=
      ( clo cco cin wcel cv cfv csp co wceq chil wral wreu wrex cnlnadjeu
      reurex syl ) DEFGZHAIZDJBIZKLUBUCCIJKLMBNOANOZCUAPUDCUAQABCDRUDCUAST $.
      $( [18-Feb-2006] $)
  $}

  ${
    $d t u v x y z $.
    $( Every continuous linear Hilbert space operator has an adjoint. $)
    cnlnssadj $p |- ( LinOp i^i ConOp ) C_ dom adjh $=
      ( vy vt vx vz vu vv clo cco cin cado cdm cv wcel cop wex cfv csp co wceq
      chil wral wa wrex cnlnadj df-rex sylib wf w3a inss1 sseli lnopf syl a1d
      wi a1i adantrd eqcom biimpi ralimi wb adjsym syl2an ancoms syl5ib
      expimpd 3jcad copab dfadj2 eleq2i visset feq1 fveq1 opreq2d eqeq1d
      2ralbidv 3anbi13d opreq1d eqeq2d 3anbi23d opelopab bitr2i syl6ib eximdv
      mpd eldm2 sylibr ssriv ) AGHIZJKZALZWHMZWJBLZNZJMZBOZWJWIMWKWLWHMZCLZWJPD
      LZQRZWQWRWLPQRZSZDTUAZCTUAZUBZBOZWOWKXCBWHUCXECDBWJUDXCBWHUEUFWKXDWNBWKXD
      TTWJUGZTTWLUGZWQWRWJPZQRZWQWLPZWRQRZSZDTUACTUAZUHZWNWKXDXFXGXMWKXFXDWKWJG
      MXFWHGWJGHUIZUJWJUKULZUMWKWPXGXCWPXGUNWKWPWLGMXGWHGWLXOUJWLUKULZUOUPWKWPX
      CXMXCWTWSSZDTUAZCTUAZWKWPUBXMXBXSCTXAXRDTXAXRWSWTUQURUSUSWPWKXTXMUTZWPXGX
      FYAWKXQXPCDWLWJVAVBVCVDVEVFWNWMTTELZUGZTTFLZUGZWQWRYBPZQRZWQYDPZWRQRZSZDT
      UACTUAZUHZEFVGZMXNJYMWMCDFEVHVIYLXFYEXIYISZDTUACTUAZUHXNEFWJWLAVJZBVJYBWJ
      SZYCXFYKYOYETTYBWJVKYQYJYNCDTTYQYGXIYIYQYFXHWQQWRYBWJVLVMVNVOVPYDWLSZYEXG
      YOXMXFTTYDWLVKYRYNXLCDTTYRYIXKXIYRYHXJWRQWQYDWLVLVQVRVOVSVTWAWBWCWDBWJJYP
      WEWFWG $.
      $( [18-Feb-2006] $)
  $}


  $( Every bounded linear Hilbert space operator has an adjoint. $)
  bdopssadj $p |- BndLinOp C_ dom adjh $=
    ( cbo clo cco cin cado cdm lncnbd cnlnssadj eqsstr3i ) ABCDEFGHI $.
    $( [19-Feb-2006] $)

  $( Every bounded linear Hilbert space operator has an adjoint. $)
  bdopadj $p |- ( T e. BndLinOp -> T e. dom adjh ) $=
    ( cbo cado cdm bdopssadj sseli ) BCDAEF $.
    $( [22-Feb-2006] $)

  ${
    $d t w x y z T $.
    $( The adjoint of a bounded linear operator is a bounded linear
       operator. $)
    adjbdln $p |- ( T e. BndLinOp -> ( adjh ` T ) e. BndLinOp ) $=
      ( vx vy vt cbo wcel cado cfv cv csp co wceq chil wral crab cuni cmap wf
      wa cab df-rab ax-hilex elmap anbi1i abbii eqtri unieqi a1i wrex wmo clo
      cco cin cnlnadj lncnopbd wb lncnbd rexeq ax-mp 3imtr3i adjmo ax-17 bdopf
      adjsym sylan pm5.32da eqcom 2ralbii anbi12i syl6rbbr mobid mpbiri wss
      sylibr ssriv mouniss mp3an1 syl11anc adjval2 syl 3eqtr4rd wreu cnlnadjeu
      reueq1 reucl eqeltrd ) AEFZAGHZBIZAHCIZJKZWIWJDIZHJKZLZCMNBMNZDEOPZEWGWOD
      MMQKZOZPZMMWLRZWOSZDTZPZWPWHWSXCLWGWRXBWRWLWQFZWOSZDTXBWODWQUAXEXADXDWTWO
      MMWLUBUBUCZUDUEUFUGUHWGWODEUIZXEDUJZWPWSLZAUKULUMZFZWODXJUIZWGXGBCDAUNAUO
      ZXJELZXLXGUPUQWODXJEURUSUTWGXHWTWIWJAHJKWIWLHWJJKLCMNBMNZSZDUJBCDAVAWGXEX
      PDWGDVBWGXPWTWMWKLZCMNBMNZSXEWGWTXOXRWGMMARZWTXOXRUPAVCZBCAWLVDVEVFXDWTWO
      XRXFWNXQBCMMWKWMVGVHVIVJVKVLEWQVMXGXHXIDEWQWLEFWTXDWLVCXFVNVOWODEWQVPVQVR
      WGXSWHXCLXTBCDAVSVTWAWGWODEWBZWPEFXKWODXJWBZWGYABCDAWCXMXNYBYAUPUQWODXJEW
      DUSUTWODEWEVTWF $.
      $( [19-Feb-2006] $)

    $( An operator is bounded and linear iff its adjoint is. $)
    adjbdlnb $p |- ( T e. BndLinOp <-> ( adjh ` T ) e. BndLinOp ) $=
      ( cbo wcel cado cfv adjbdln cdm bdopadj dmadjrnb sylibr wa ccom cid cres
      wceq ccnv cnvadj coeq1i wf1o adj1o f1ococnv1 ax-mp eqtr3i fveq1i a1i
      wfun funadj fvco mp3an12 adantr fvresi 3eqtr3rd adantl eqeltrd mpancom
      impbii ) ABCZADEZBCZAFADGZCZUSUQUSURUTCVAURHAIJVAUSKZAURDEZBVBADDLZEZAMUT
      NZEZVCAVEVGOVBAVDVFDPZDLZVDVFVHDDQRUTUTDSVIVFOTUTUTDUAUBUCUDUEVAVEVCOZUSD
      UFZVKVAVJUGUGADDUHUIUJVAVGAOUSUTAUKUJULUSVCBCVAURFUMUNUOUP $.
      $( [19-Feb-2006] $)

    $( The mapping of adjoints of bounded linear operators is one-to-one
       onto. $)
    adjbd1o $p |- ( adjh |` BndLinOp ) : BndLinOp -1-1-onto-> BndLinOp $=
      ( vy vx cbo cado cima cres wf1o cdm wf1 wss adj1o f1of1 ax-mp bdopssadj
      f1ores mp2an wceq wb cv wcel wbr wrex cfv visset elima bdopadj wfn f1ofn
      fnbrfvb mpan syl rexbiia adjbdlnb eleq1 syl5bb biimpcd r19.23aiv adjbdln
      adjadj fveq2 eqeq1d rcla4ev syl11anc impbii 3bitr2i eqriv f1oeq3 mpbi )
      CDCEZDCFZGZCCVJGZDHZVMDIZCVMJVKVMVMDGZVNKVMVMDLMNVMVMCDOPVICQVKVLRAVICASZ
      VITBSZVPDUAZBCUBVQDUCZVPQZBCUBZVPCTZBVPDCAUDZUEVTVRBCVQCTZVQVMTZVTVRRZVQU
      FDVMUGZWEWFVOWGKVMVMDUHMVMVQVPDWCUIUJUKULWAWBVTWBBCVTWDWBWDVSCTVTWBVQUMVS
      VPCUNUOUPUQWBVPDUCZCTWHDUCZVPQZWAVPURWBVPVMTWJVPUFVPUSUKVTWJBWHCVQWHQVSWI
      VPVQWHDUTVAVBVCVDVEVFVICCVJVGMVH $.
      $( [20-Oct-2006] $) $( [19-Feb-2006] $)

    $( The adjoint of an operator is linear.  Proposition 1 of
       [AkhiezerGlazman] p. 80. $)
    adjlnop $p |- ( T e. dom adjh -> ( adjh ` T ) e. LinOp ) $=
      ( vx vy vz vw cado cdm wcel chil cfv wf cv csm co cva wceq wral cc clo
      dmadjrn dmadjop syl wa csp w3a caddc ffvelrn sylan 3adant3 hvmulcl
      adantr 3ad2ant3 simp3r his7 syl111anc hvaddcl adj2 syl3an3 ccj cmul
      3adant3l opreq2d simp3l his5 simp2 adjcl adantrl 3adant2 3eqtr4d
      3adant3r opreq12d sylan2 an12s adantrr eqtr4d 3eqtr3d 3com23 3expa
      r19.21aiva wb syl2an anandis hial2eq2 syl11anc mpbid exp32 r19.21adv
      r19.21aivv ellnop sylanbrc ) AFGZHZIIAFJZKZBLZCLZMNZDLZONZWMJZWOWPWMJZMNZ
      WRWMJZONZPZDIQZCIQBRQWMSHWLWMWKHWNATWMUAUBWLXFBCRIWLWORHZWPIHZUCZXEDIWLXI
      WRIHZXEWLXIXJUCZUCZELZWTUDNZXMXDUDNZPZEIQZXEXLXPEIWLXKXMIHZXPWLXRXKXPWLXR
      XKUEZXMAJZWSUDNZXTWQUDNZXTWRUDNZUFNZXNXOXSXTIHZWQIHZXJYAYDPWLXRYEXKWLIIAK
      XRYEAUAIIXMAUGUHZUIXKWLYFXRXIYFXJWOWPUJZUKULWLXRXIXJUMXTWQWRUNUOXKWLXRWSI
      HZYAXNPXIYFXJYIYHWQWRUPUHZXMWSAUQURXSYDXMXBUDNZXMXCUDNZUFNZXOXSYBYKYCYLUF
      WLXRXIYBYKPXJWLXRXIUEZWOUSJZXTWPUDNZUTNZYOXMXAUDNZUTNZYBYKYNYPYRYOUTWLXRX
      HYPYRPXGXMWPAUQVAVBYNXGYEXHYBYQPWLXRXGXHVCZWLXRYEXIYGUIWLXRXGXHUMWOXTWPVD
      UOYNXGXRXAIHZYKYSPYTWLXRXIVEWLXIUUAXRWLXHUUAXGWPAVFZVGVHWOXMXAVDUOVIVJWLX
      RXJYCYLPXIXMWRAUQVAVKXSXRXBIHZXCIHZXOYMPWLXRXKVEWLXKUUCXRWLXIUUCXJXGWLXHU
      UCWLXHUCXGUUAUUCUUBWOXAUJVLVMZVNVHWLXKUUDXRWLXJUUDXIWRAVFZVGVHXMXBXCUNUOV
      OVPVQVRVSXLWTIHZXDIHZXQXEVTXKWLYIUUGYJWSAVFVLWLXIXJUUHWLXIUCUUCUUDUUHWLXJ
      UCUUEUUFXBXCUPWAWBEWTXDWCWDWEWFWGWHBCDWMWIWJ $.
      $( [17-Jun-2006] $)

    $( Every operator with an adjoint is linear. $)
    adjsslnop $p |- dom adjh C_ LinOp $=
      ( vt cado cdm clo cv wcel cfv adjadj dmadjrn adjlnop syl eqeltrrd ssriv
      ) ABCZDAEZNFZOBGZBGZODOHPQNFRDFOIQJKLM $.
      $( [17-Jun-2006] $)
  $}

  ${
    $d f g h u v w z A $.  $d f g h t u v w x y z T $.
    nmopadjle.1 $e |- T e. BndLinOp $.
    $( Property of the norm of an adjoint.  Part of proof of Theorem 3.10 of
       [Beran] p. 104. $)
    nmopadjlei $p |- ( A e. ~H -> ( normh ` ( ( adjh ` T ) ` A ) ) <_
                        ( ( normop ` T ) x. ( normh ` A ) ) ) $=
      ( vz vu vv vf vw vg vh chil wcel cado cfv cno cv csp co wceq wral crab
      cuni wa copab cnop cmul cle cdm cbo bdopssadj sselii adjvalval mpan
      opreq2 eqeq1d ralbidv rabbidv unieqd eqid ax-hilex rabex uniex fvopab4
      eqtr4d fveq2d clo cco cin inss1 lncnbd eleqtrri inss2 eqeq2d cbvrabv
      unieqi cnlnadjlem7 eqbrtrd ) AKLZABMNNZONADPZKLEPFPZBNZVTQRZWAGPZQRZSZFKT
      ZGKUAZUBZSUCDEUDZNZONBUENAONUFRUGVRVSWKOVRVSWBAQRZWESZFKTZGKUAZUBZWKBMUHZ
      LVRVSWPSUIWQBUJCUKFGABULUMDEAWIWPKWJVTASZWHWOWRWGWNGKWRWFWMFKWRWCWLWEVTAW
      BQUNUOUPUQURWJUSZWOWNGKUTVAVBVCVDVEDHFEAWIBIJWJIPZKLJPWTBNVTQRSUCIJUDZVFV
      GVHZVFBVFVGVIBUIXBCVJVKZUKXBVGBVFVGVLXCUKXAUSWHWCWAHPZQRZSZFKTZHKUAWGXGGH
      KWDXDSZWFXFFKXHWEXEWCWDXDWAQUNVMUPVNVOWSVPVQ $.
      $( [22-Feb-2006] $)

    $( Lemma for ~ nmopadji . $)
    nmopadjlem $p |- ( normop ` ( adjh ` T ) ) <_ ( normop ` T ) $=
      ( vy chil cado cfv wf cnop cxr wcel cv cno c1 cle wbr wi wral cbo
      adjbdln bdopf mp2b nmopxr wa cmul co cr ffvelrni normcl syl adantr
      nmopre ax-mp remulcl sylancr 1re remulcli a1i nmopadjlei cc0 nmopge0
      pm3.2i lemul2a mp3anl3 mpanl2 sylan letrd recni mulid1i syl6breq ex rgen
      nmopub mp3an ) DDAEFZGZAHFZIJZCKZLFZMNOZVRVNFZLFZVPNOZPZCDQVNHFVPNOARJZVN
      RJVOBASVNTUAZWEDDAGZVQBATZAUBUAWDCDVRDJZVTWCWIVTUCZWBVPMUDUEZVPNWJWBVPVSU
      DUEZWKWIWBUFJZVTWIWADJWMDDVRVNWFUGWAUHUIUJWIWLUFJZVTWIVPUFJZVSUFJZWNWEWOB
      AUKULZVRUHZVPVSUMUNUJWKUFJWJVPMWQUOUPUQWIWBWLNOVTVRABURUJWIWPVTWLWKNOZWRW
      PMUFJZVTWSUOWPWTWOUSVPNOZUCVTWSWOXAWQWEWGXABWHAUTUAVAVSMVPVBVCVDVEVFVPVPW
      QVGVHVIVJVKCVPVNVLVM $.
      $( [22-Feb-2006] $)

    $( Property of the norm of an adjoint.  Theorem 3.11(v) of [Beran]
       p. 106. $)
    nmopadji $p |- ( normop ` ( adjh ` T ) ) = ( normop ` T ) $=
      ( cado cfv cnop wceq cle wbr cbo wcel cr adjbdln ax-mp nmopre letri3i
      nmopadjlem cdm bdopadj adjadj fveq2i eqbrtrri mpbir2an ) ACDZEDZAEDZFUDUE
      GHUEUDGHUDUEUCIJZUDKJAIJZUFBALMZUCNMUGUEKJBANMOABPUCCDZEDUEUDGUIAEACQJZUI
      AFUGUJBARMASMTUCUHPUAUB $.
      $( [22-Feb-2006] $)
  $}

  $( An operator is zero iff its adjoint is zero.  Theorem 3.11(i) of [Beran]
     p. 106. $)
  adjeq0 $p |- ( T = 0hop <-> ( adjh ` T ) = 0hop ) $=
    ( ch0o wceq cado cfv fveq2 adj0 syl6eq cdm wcel cbo bdopssadj 0bdop sselii
    eleq1 mpbiri dmadjrnb sylibr adjadj syl a1i 3eqtr3d impbii ) ABCZADEZBCZUDU
    EBDEZBABDFGHUFUEDEZUGABUEBDFUFADIZJZUHACUFUEUIJZUJUFUKBUIJKUIBLMNUEBUIOPAQR
    ASTUGBCUFGUAUBUC $.
    $( [30-Jun-2008] $) $( [20-Feb-2006] $)

  ${
    $d x y A $.  $d x y T $.
    $( The adjoint of the scalar product of an operator.  Theorem 3.11(ii) of
       [Beran] p. 106. $)
    adjmul $p |- ( ( A e. CC /\ T e. dom adjh ) ->
                 ( adjh ` ( A .op T ) ) = ( ( * ` A ) .op ( adjh ` T ) ) ) $=
      ( vx vy cc wcel cado cdm wa chil chot co wf ccj cfv cv csp wceq wral
      dmadjop homulcl sylan2 cjcl dmadjrn syl syl2an csm cmul adj2 3expb
      adantll opreq2d wi ffvelrn sylan ax-his3 syl3an2 3exp exp3a imp43 simpll
      simprl adjcl ad2ant2l his52 syl111anc 3eqtr4d homval 3expa adantrr
      opreq1d id syl3an adantrl ex r19.21aivv adjeq ) AEFZBGHZFZIZJJABKLZMZJJAN
      OZBGOZKLZMZCPZWBOZDPZQLZWHWJWFOZQLZRZDJSCJSWBGOWFRVTVRJJBMZWCBTZABUAUBVRW
      DEFZJJWEMZWGVTAUCZVTWEVSFWRBUDWETUEZWDWEUAUFWAWNCDJJWAWHJFZWJJFZIZWNWAXCI
      ZAWHBOZUGLZWJQLZWHWDWJWEOZUGLZQLZWKWMXDAXEWJQLZUHLZAWHXHQLZUHLZXGXJXDXKXM
      AUHVTXCXKXMRZVRVTXAXBXOWHWJBUIUJUKULVRVTXAXBXGXLRZVRVTXAXBXPUMVRVTXAIZXBX
      PXQVRXEJFZXBXPVTWOXAXRWPJJWHBUNUOAXEWJUPUQURUSUTXDVRXAXHJFZXJXNRVRVTXCVAW
      AXAXBVBVTXBXSVRXAWJBVCVDAWHXHVEVFVGXDWIXFWJQWAXAWIXFRZXBVRVTXAXTVTVRWOXAX
      TWPAWHBVHUQVIVJVKXDWLXIWHQWAXBWLXIRZXAVRVTXBYAVRWQVTWRXBXBYAWSWTXBVLWDWJW
      EVHVMVIVNULVGVOVPCDWFWBVQVF $.
      $( [5-Nov-2006] $) $( [21-Feb-2006] $)
  $}

  ${
    $d x y S $.  $d x y T $.
    $( The adjoint of the sum of two operators.  Theorem 3.11(iii) of [Beran]
       p. 106. $)
    adjadd $p |- ( ( S e. dom adjh /\ T e. dom adjh ) ->
                ( adjh ` ( S +op T ) ) = ( ( adjh ` S ) +op ( adjh ` T ) ) ) $=
      ( vx vy cado cdm wcel wa chil chos co wf cfv cv csp wceq wral dmadjop
      hoaddcl syl2an dmadjrn syl cva caddc ffvelrn sylan ad2ant2r ad2ant2lr
      simprr ax-his2 syl111anc adj2 3expb adantlr adantll opreq12d eqtrd
      anim12i hosval 3expa adantrr opreq1d simprl adjcl ad2ant2rl ad2ant2l
      his7 3eqtr4d adantrl opreq2d eqtr4d r19.21aivva adjeq ) AEFZGZBVNGZHZIIAB
      JKZLZIIAEMZBEMZJKZLZCNZVRMZDNZOKZWDWFWBMZOKZPZDIQCIQVREMWBPVOIIALZIIBLZVS
      VPARZBRZABSTVOIIVTLZIIWALZWCVPVOVTVNGWOAUAVTRUBZVPWAVNGWPBUAWARUBZVTWASTV
      QWJCDIIVQWDIGZWFIGZHZHZWGWDWFVTMZWFWAMZUCKZOKZWIXBWDAMZWDBMZUCKZWFOKZWDXC
      OKZWDXDOKZUDKZWGXFXBXJXGWFOKZXHWFOKZUDKZXMXBXGIGZXHIGZWTXJXPPVOWSXQVPWTVO
      WKWSXQWMIIWDAUEUFUGVPWSXRVOWTVPWLWSXRWNIIWDBUEUFUHVQWSWTUIXGXHWFUJUKXBXNX
      KXOXLUDVOXAXNXKPZVPVOWSWTXSWDWFAULUMUNVPXAXOXLPZVOVPWSWTXTWDWFBULUMUOUPUQ
      XBWEXIWFOVQWSWEXIPZWTVQWKWLHWSYAVOWKVPWLWMWNURWKWLWSYAWDABUSUTUFVAVBXBWSX
      CIGZXDIGZXFXMPVQWSWTVCVOWTYBVPWSWFAVDVEVPWTYCVOWSWFBVDVFWDXCXDVGUKVHXBWHX
      EWDOVQWTWHXEPZWSVQWOWPHWTYDVOWOVPWPWQWRURWOWPWTYDWFVTWAUSUTUFVIVJVKVLCDWB
      VRVMUK $.
      $( [22-Feb-2006] $)
  $}

  ${
    $d x y z S $.  $d x y z T $.
    nmoptri.1 $e |- S e. BndLinOp $.
    nmoptri.2 $e |- T e. BndLinOp $.
    $( Triangle inequality for the norms of bounded linear operators. $)
    nmoptrii $p |- ( normop ` ( S +op T ) ) <_
                  ( ( normop ` S ) + ( normop ` T ) ) $=
      ( vx chil chos co wf cnop cfv caddc cxr wcel cv cno c1 cle wbr wi wral
      cbo bdopf ax-mp hoaddcli cr nmopre readdcli rexr wa hoscli normcl syl
      adantr ffvelrni readdcl syl11anc a1i cva wceq hosval mp3an12 fveq2d
      norm-ii eqbrtrd nmoplb mp3an1 le2add mpanr12 mp2and letrd ex rgen nmopub
      mp3an ) FFABGHZIAJKZBJKZLHZMNZEOZPKQRSZWAVPKZPKZVSRSZTZEFUAVPJKVSRSABAUBN
      ZFFAIZCAUCUDZBUBNZFFBIZDBUCUDZUEVSUFNZVTVQVRWGVQUFNZCAUGUDZWJVRUFNZDBUGUD
      ZUHZVSUIUDWFEFWAFNZWBWEWSWBUJZWDWAAKZPKZWABKZPKZLHZVSWSWDUFNZWBWSWCFNXFWA
      ABWIWLUKWCULUMUNWSXEUFNZWBWSXBUFNZXDUFNZXGWSXAFNZXHFFWAAWIUOZXAULUMZWSXCF
      NZXIFFWABWLUOZXCULUMZXBXDUPUQUNWMWTWRURWSWDXERSWBWSWDXAXCUSHZPKZXERWSWCXP
      PWHWKWSWCXPUTWIWLWAABVAVBVCWSXJXMXQXERSXKXNXAXCVDUQVEUNWTXBVQRSZXDVRRSZXE
      VSRSZWHWSWBXRWIWAAVFVGWKWSWBXSWLWABVFVGWSXRXSUJXTTZWBWSXHXIYAXLXOXHXIUJWN
      WPYAWOWQXBXDVQVRVHVIUQUNVJVKVLVMEVSVPVNVO $.
      $( [10-Mar-2006] $)

    $( Upper bound for the norm of the composition of two bounded linear
       operators. $)
    nmopcoi $p |- ( normop ` ( S o. T ) ) <_
                   ( ( normop ` S ) x. ( normop ` T ) ) $=
      ( vx chil ccom wf cnop cfv cmul co cxr wcel cv cno c1 cle wbr wi wral
      cbo clo bdopln ax-mp lnopcoi lnopfi cr nmopre remulcli rexr cc0 wceq wa
      0re leidi a1i ch0o lnopco0i nmlnop0iHIL sylib fveq1 fveq2d c0v ho0val
      norm0 syl6eq sylan9eq sylan opreq2 recni mul01i adantr 3brtr4d adantrr
      wn wne df-ne cdiv csm cabs cc recclzi ffvelrni norm-iii syl2an bdopf
      lnopmuli hocoi opreq2d adantl eqtr4d normcl syl recnd divrec2 mp3an2
      ancoms rerecclzi clt wb nmopgt0 recgt0i sylbi ltle mpan sylc absid
      syl11anc opreq1d 3eqtr4rd hvmulcl 3eqtr4d nmoplb mp3an1 mulid2i
      syl6breqr biimpi 1re ledivmul2OLD mp3anl2 mpanl2 mpbird eqbrtrd w3a 3jca
      ad2antrl mpbid sylanbr pm2.61ian ex rgen nmopub mp3an ) FFABGZHAIJZBIJZKL
      ZMNZEOZPJQRSZYTYOJZPJZYRRSZTZEFUAYOIJZYRRSYOABAUBNZAUCNCAUDUEZBUBNZBUCNDB
      UDUEZUFZUGZYRUHNYSYPYQUUGYPUHNZCAUIUEZUUIYQUHNZDBUIUEZUJYRUKUEUUEEFYTFNZU
      UAUUDYQULUMZUUQUUAUNZUUDUURUUQUUDUUAUURUUQUNZULULUUCYRRULULRSUUTULUOUPUQU
      URYOURUMZUUQUUCULUMUURUUFULUMUVAABUUHUUJUSYOUUKUTVAUVAUUQUUCYTURJZPJZULUV
      AUUBUVBPYTYOURVBVCUUQUVCVDPJULUUQUVBVDPYTVEVCVFVGVHVIUURYRULUMUUQUURYRYPU
      LKLULYQULYPKVJYPYPUUNVKVLVGVMVNVOUURVPYQULVQZUUSUUDYQULVRUVDUUSUNZUUCYQVS
      LZYPRSZUUDUVEUVFQYQVSLZYTBJZVTLZAJZPJZYPRUVDUUQUVFUVLUMUUAUVDUUQUNZUVHUUB
      VTLZPJZUVHWAJZUUCKLZUVLUVFUVDUVHWBNZUUBFNZUVOUVQUMUUQYQYQUUPVKZWCZFFYTYOU
      ULWDZUVHUUBWEWFUVMUVKUVNPUVMUVKUVHUVIAJZVTLZUVNUVDUVRUVIFNZUVKUWDUMUUQUWA
      FFYTBUUIFFBHZDBWGUEZWDZUVHUVIAUUHWHWFUUQUVNUWDUMUVDUUQUUBUWCUVHVTYTABUUGF
      FAHZCAWGUEZUWGWIWJWKWLVCUVMUVFUVHUUCKLZUVQUUQUVDUVFUWKUMZUUQUUCWBNZUVDUWL
      UUQUUCUUQUVSUUCUHNZUWBUUBWMWNZWOUWMYQWBNZUVDUWLUVTUUCYQWPWQVIWRUVMUVPUVHU
      UCKUVDUVPUVHUMZUUQUVDUVHUHNZULUVHRSZUWQYQUUPWSZUVDUWRULUVHWTSZUWSUWTUVDUL
      YQWTSZUXAUWFUVDUXBXAUWGBXBUEZYQUUPXCXDULUHNUWRUXAUWSTUOULUVHXEXFXGUVHXHXI
      VMZXJWLXKVOUVEUVJFNZUVJPJZQRSZUVLYPRSZUVDUUQUXEUUAUVDUVRUWEUXEUUQUWAUWHUV
      HUVIXLWFVOUVEUXFUVIPJZYQVSLZQRUVDUUQUXFUXJUMUUAUVMUVPUXIKLZUVHUXIKLZUXFUX
      JUVMUVPUVHUXIKUXDXJUVDUVRUWEUXFUXKUMUUQUWAUWHUVHUVIWEWFUUQUVDUXJUXLUMZUUQ
      UXIWBNZUVDUXMUUQUXIUUQUWEUXIUHNZUWHUVIWMWNZWOUXNUWPUVDUXMUVTUXIYQWPWQVIWR
      XMVOUVEUXJQRSZUXIQYQKLZRSZUUSUXSUVDUUSUXIYQUXRRUWFUUQUUAUXIYQRSUWGYTBXNXO
      YQUVTXPXQWKUVDUUQUXQUXSXAZUUAUUQUVDUXTUUQUXOUXBUXTUVDUXPUVDUXBUXCXRZUXOQU
      HNZUXBUXTXSUXOUUOUYBUXBUXTUUPUXIYQQXTYAYBWFWRVOYCYDUWIUXEUXGUXHUWJUVJAXNX
      OXIYDUVEUWNUUOUUMYEZUXBUVGUUDXAUUQUYCUVDUUAUUQUWNUUOUUMUWOUUOUUQUUPUQUUMU
      UQUUNUQYFYGUVDUXBUUSUYAVMUUCYQYPXTXIYHYIYJYKYLEYRYOYMYN $.
      $( [10-Mar-2006] $)

    $( The sum of two bounded linear operators is a bounded linear operator. $)
    bdophsi $p |- ( S +op T ) e. BndLinOp $=
      ( chos co cbo wcel clo cnop cfv cr elbdop2 bdopln ax-mp lnophsi cxr
      caddc cmnf clt wbr cle chil wf bdopf hoaddcli nmopxr nmopre readdcli
      nmopgtmnf nmoptrii xrre mp4an mpbir2an ) ABEFZGHUOIHUOJKZLHZUOMABAGHZAIHC
      ANOBGHZBIHDBNOPUPQHZAJKZBJKZRFZLHSUPTUAZUPVCUBUAUQUCUCUOUDZUTABURUCUCAUDC
      AUEOUSUCUCBUDDBUEOUFZUOUGOVAVBURVALHCAUHOUSVBLHDBUHOUIVEVDVFUOUJOABCDUKUP
      VCULUMUN $.
      $( [9-Mar-2006] $)

    $( The difference between two bounded linear operators is bounded. $)
    bdophdi $p |- ( S -op T ) e. BndLinOp $=
      ( c1 cneg chot co chos chod cbo wcel chil wf bdopf ax-mp honegsubi cc
      ax-1cn negcli bdophmi bdophsi eqeltrri ) AEFZBGHZIHABJHKABAKLMMANCAOPBKLM
      MBNDBOPQAUECUDRLUEKLESTUDBDUAPUBUC $.
      $( [10-Mar-2006] $)

    $( The composition of two bounded linear operators is bounded. $)
    bdopcoi $p |- ( S o. T ) e. BndLinOp $=
      ( ccom cbo wcel clo cnop cfv cr elbdop2 bdopln ax-mp lnopcoi cxr cmul co
      cmnf clt wbr cle chil wf lnopfi hocofi nmopxr nmopre remulcli nmopgtmnf
      nmopcoi xrre mp4an mpbir2an ) ABEZFGUOHGUOIJZKGZUOLABAFGZAHGCAMNZBFGZBHGD
      BMNZOUPPGZAIJZBIJZQRZKGSUPTUAZUPVEUBUAUQUCUCUOUDZVBABAUSUEBVAUEUFZUOUGNVC
      VDURVCKGCAUHNUTVDKGDBUHNUIVGVFVHUOUJNABCDUKUPVEULUMUN $.
      $( [9-Mar-2006] $)

    $( Triangle-type inequality for the norms of bounded linear operators. $)
    nmoptri2i $p |- ( ( normop ` S ) - ( normop ` T ) ) <_
                  ( normop ` ( S +op T ) ) $=
      ( cnop cfv cmin co chos cle wbr caddc c1 cneg chot bdophsi cc wcel cbo
      ax-1cn negcli bdophmi ax-mp nmoptrii chod chil wf bdopf hoaddcli
      honegsubi hopncani eqtri fveq2i nmopnegi opreq2i 3brtr3i cr nmopre
      lesubaddi mpbir ) AEFZBEFZGHABIHZEFZJKVAVDVBLHZJKVCMNZBOHZIHZEFVDVGEFZLHV
      AVEJVCVGABCDPZVFQRVGSRMTUAVFBDUBUCUDVHAEVHVCBUEHAVCBABASRZUFUFAUGCAUHUCZB
      SRZUFUFBUGDBUHUCZUIVNUJABVLVNUKULUMVIVBVDLBVNUNUOUPVAVBVDVKVAUQRCAURUCVMV
      BUQRDBURUCVCSRVDUQRVJVCURUCUSUT $.
      $( [26-Nov-2006] $) $( [10-Mar-2006] $)

    $( The adjoint of a composition of bounded linear operators.  Theorem
       3.11(viii) of [Beran] p. 106. $)
    adjcoi $p |- ( adjh ` ( S o. T ) ) = ( ( adjh ` T ) o. ( adjh ` S ) ) $=
      ( vx vy cv ccom cado cfv csp co wceq chil wral wcel wa cbo wf adjbdln
      ax-mp bdopf hocoi opreq2d adantl opreq1d adantr ffvelrni cdm bdopadj
      adj2 mp3an1 sylan sylan2 3eqtrd bdopcoi 3eqtr2rd rgen2a wb hocofi hoeq2
      mp2an mpbi ) EGZFGZABHZIJZJKLZVDVEBIJZAIJZHZJZKLZMZFNOENOZVGVKMZVNEFNVDNP
      ZVENPZQZVMVDVEVJJZVIJZKLZVDVFJZVEKLZVHVRVMWBMVQVRVLWAVDKVEVIVJVIRPZNNVISB
      RPZWEDBTUAVIUBUAZVJRPZNNVJSARPZWHCATUAVJUBUAZUCUDUEVSWDVDBJZAJZVEKLZWKVTK
      LZWBVQWDWMMVRVQWCWLVEKVDABWINNASCAUBUAWFNNBSDBUBUAZUCUFUGVQWKNPZVRWMWNMZN
      NVDBWOUHAIUIZPZWPVRWQWIWSCAUJUAWKVEAUKULUMVRVQVTNPZWNWBMZNNVEVJWJUHBWRPZV
      QWTXAWFXBDBUJUAVDVTBUKULUNUOVFWRPZVQVRWDVHMVFRPZXCABCDUPZVFUJUAVDVEVFUKUL
      UQURNNVGSZNNVKSVOVPUSVGRPZXFXDXGXEVFTUAVGUBUAVIVJWGWJUTEFVGVKVAVBVC $.
      $( [10-Mar-2006] $)
  $}

  ${
    $d x y S $.  $d x y T $.
    nmopcoadj.1 $e |- T e. BndLinOp $.
    $( The norm of an operator composed with its adjoint.  Part of Theorem
       3.11(vi) of [Beran] p. 106. $)
    nmopcoadji $p |- ( normop ` ( ( adjh ` T ) o. T ) )
                    = ( ( normop ` T ) ^ 2 ) $=
      ( vx cado cfv ccom cnop c2 cexp co wceq cle wbr cbo wcel cr adjbdlnb
      mpbi bdopcoi nmopre ax-mp resqcli letri3i chil wf cxr cv cno c1 wi wral
      bdopf hocofi rexr wa cmul hocoi fveq2d adantr ffvelrni normcl 3syl syl
      remulcl sylancr remulcli a1i nmbdoplbi 1re cc0 nmopge0 mp2b pm3.2i
      lemul2a mp3anl3 mpanl2 sylan recni mulid1i syl6breq letrd syl21anc
      eqbrtrd nmopadji opreq1i sqvali eqtr4i ex rgen nmopub mp3an csqr sqrcli
      csp cabs cc hicl mpancom abscl syl11anc bcs hococli normge0 jca simpr
      mp3anl2 recnd mulid1 eqtr4d breqtrd resqcl sqge0 absid normsq cdm
      bdopadj adj2 mp3an1 adjadj fveq1i opreq2i syl6req eqtrd eqtr3d sqsqri
      3brtr4d wb sqrge0i le2sq mpanr12 mpbird le2sqi mp2an breqtri mpbir2an )
      ADEZAFZGEZAGEZHIJZKYRYTLMZYTYRLMYRYTYQNOYRPOZYPAANOZYPNOZBAQRZBSZYQTUAZYS
      UUCYSPOZBATUAZUBZUCUDUDYQUEZYTUFOZCUGZUHEZUILMZUUMYQEZUHEZYTLMZUJZCUDUKUU
      AYPAUUDUDUDYPUEZUUEYPULZUAZUUCUDUDAUEZBAULZUAZUMZYTPOUULUUJYTUNUAUUSCUDUU
      MUDOZUUOUURUVGUUOUOZUUQYPGEZYSUPJZYTLUVHUUQUUMAEZYPEZUHEZUVJLUVGUUQUVMKUU
      OUVGUUPUVLUHUUMYPAUVBUVEUQURZUSUVHUVMUVIUVKUHEZUPJZUVJUVGUVMPOZUUOUVGUVKU
      DOZUVLUDOZUVQUDUDUUMAUVEUTZUDUDUVKYPUVBUTZUVLVAVBZUSUVGUVPPOZUUOUVGUVIPOZ
      UVOPOZUWCUUDUWDUUEYPTUAZUVGUVRUWEUVTUVKVAZVCZUVIUVOVDVEUSUVJPOUVHUVIYSUWF
      UUIVFVGUVGUVMUVPLMZUUOUVGUVRUWIUVTUVKYPUUEVHVCUSUVHUWEUUHUVOYSLMZUVPUVJLM
      ZUVGUWEUUOUWHUSZUUHUVHUUIVGZUVHUVOYSUUNUPJZYSUWLUVGUWNPOZUUOUVGUUHUUNPOZU
      WOUUIUUMVAZYSUUNVDVEUSUWMUVGUVOUWNLMUUOUUMABVHUSUVHUWNYSUIUPJZYSLUVGUWPUU
      OUWNUWRLMZUWQUWPUIPOZUUOUWSVIUWPUWTUUHVJYSLMZUOUUOUWSUUHUXAUUIUUCUVCUXABU
      VDAVKVLZVMUUNUIYSVNVOVPVQYSYSUUIVRZVSVTWAUWEUUHUWDVJUVILMZUOUWJUWKUWDUXDU
      WFUUDUUTUXDUUEUVAYPVKVLVMUVOYSUVIVNVOWBWAWCUVJYSYSUPJYTUVIYSYSUPABWDWEYSU
      XCWFWGVTWHWICYTYQWJWKYTYRWLEZHIJZYRLYSUXELMZYTUXFLMZUVCUXEUFOZUUOUVOUXELM
      ZUJZCUDUKUXGUVEVJYRLMZUXEPOZUXIUUKUXLUVFYQVKZUAZYRUUGWMZUXEUNVLUXKCUDUVGU
      UOUXJUVHUXJUVOHIJZUXFLMZUVHUVLUUMWNJZWOEZYRUXQUXFLUVHUXTUVMUUNUPJZYRUVGUX
      TPOZUUOUVGUXSWPOZUYBUVSUVGUYCUVGUVRUVSUVTUWAVCZUVLUUMWQWRUXSWSVCUSUVGUYAP
      OZUUOUVGUVQUWPUYEUWBUWQUVMUUNVDWTUSZUUBUVHUUGVGZUVGUXTUYALMZUUOUVSUVGUYHU
      YDUVLUUMXAWRUSUVHUYAUUQYRUYFUVGUUQPOZUUOUVGUUPUDOUYIUUMYPAUVBUVEXBUUPVAVC
      USZUYGUVHUYAUVMUIUPJZUUQLUVHUWPUVQVJUVMLMZUOZUUOUYAUYKLMZUVGUWPUUOUWQUSUV
      GUYMUUOUVGUVQUYLUWBUVGUVRUVSUYLUVTUWAUVLXCVBXDUSUVGUUOXEUWPUWTUYMUUOUYNVI
      UUNUIUVMVNXFWBUVGUYKUUQKUUOUVGUYKUVMUUQUVGUVMWPOUYKUVMKUVGUVMUWBXGUVMXHVC
      UVNXIUSXJUVHUUQYRUUNUPJZYRUYJUVGUYOPOZUUOUVGUUBUWPUYPUUGUWQYRUUNVDVEUSUYG
      UVGUUQUYOLMUUOUUMYQUUFVHUSUVHUYOYRUIUPJZYRLUVGUWPUUOUYOUYQLMZUWQUWPUWTUUO
      UYRVIUWPUWTUUBUXLUOUUOUYRUUBUXLUUGUXOVMUUNUIYRVNVOVPVQYRYRUUGVRVSVTWAWAWA
      UVGUXQUXTKUUOUVGUXQWOEZUXQUXTUVGUVRUWEUYSUXQKZUVTUWGUWEUXQPOVJUXQLMUYTUVO
      XKUVOXLUXQXMWTVBUVGUXQUXSWOUVGUXQUVKUVKWNJZUXSUVGUVRUXQVUAKUVTUVKXNVCUVGU
      XSUVKUUMYPDEZEZWNJZVUAUVRUVGUXSVUDKZUVTYPDXOZOZUVRUVGVUEUUDVUGUUEYPXPUAUV
      KUUMYPXQXRWRVUCUVKUVKWNUUMVUBAUUCAVUFOVUBAKBAXPAXSVLXTYAYBYCURYDUSUXFYRKZ
      UVHUUKUXLVUHUVFUXNYRUUGYEVLZVGYFUVGUXJUXRYGZUUOUVGUWEVJUVOLMZVUJUWHUVGUVR
      VUKUVTUVKXCVCUWEVUKUOUXMVJUXELMZVUJUUKUXLUXMUVFUXNUXPVLZUUKUXLVULUVFUXNYR
      UUGYHVLZUVOUXEYIYJWTUSYKWHWICUXEAWJWKUXAVULUXGUXHYGUXBVUNYSUXEUUIVUMYLYMR
      VUIYNYO $.
      $( [8-Mar-2006] $)

    $( The norm of an operator composed with its adjoint.  Part of Theorem
       3.11(vi) of [Beran] p. 106. $)
    nmopcoadj2i $p |- ( normop ` ( T o. ( adjh ` T ) ) )
                    = ( ( normop ` T ) ^ 2 ) $=
      ( cado cfv ccom cnop c2 cexp co cbo wcel adjbdln ax-mp nmopcoadji cdm
      wceq bdopadj adjadj coeq1i fveq2i nmopadji opreq1i 3eqtr3i ) ACDZCDZUDEZF
      DUDFDZGHIAUDEZFDAFDZGHIUDAJKZUDJKBALMNUFUHFUEAUDACOKZUEAPUJUKBAQMARMSTUGU
      IGHABUAUBUC $.
      $( [10-Mar-2006] $)

    $( An operator composed with its adjoint is zero iff the operator is zero.
       Theorem 3.11(vii) of [Beran] p. 106. $)
    nmopcoadj0i $p |- ( ( T o. ( adjh ` T ) ) = 0hop <-> T = 0hop ) $=
      ( cado cfv ccom cnop cc0 wceq ch0o c2 cexp co nmopcoadj2i eqeq1i cbo
      wcel cr nmopre ax-mp recni sqeq0i bitri clo bdopln adjbdln lnopcoi
      nmlnop0iHIL 3bitr3i ) AACDZEZFDZGHZAFDZGHZUJIHAIHULUMJKLZGHUNUKUOGABMNUMU
      MAOPZUMQPBARSTUAUBUJAUIUPAUCPBAUDSZUIOPZUIUCPUPURBAUESUIUDSUFUGAUQUGUH $.
      $( [2-Jul-2008] $) $( [10-Mar-2006] $)
  $}

