$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Adjoints (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g h u v w y z A $.  $d u B $.  $d f C $.  $d f t w x z F $.
    $d f v w x z G $.  $d f g h t u v w x y z T $.
    cnlnadjlem.1 $e |- T e. LinOp $.
    cnlnadjlem.2 $e |- T e. ConOp $.
    cnlnadjlem.3 $e |- G =
                    { <. g , h >. | ( g e. H~ /\ h = ( ( T ` g ) .ih y ) ) } $.
    $( Lemma for ~ cnlnadj (Theorem 3.10 of [Beran] p. 104: every continuous
       linear operator has an adjoint).  The value of the auxiliary functional
       ` G ` . $)
    cnlnadjlem1 $p |- ( A e. H~ -> ( G ` A ) = ( ( T ` A ) .ih y ) ) $=
      ( cv cfv csp co chil wceq fveq2 opreq1d oprex fvopab4 ) DEBDJZCKZAJZLMBCK
      ZUBLMNFTBOUAUCUBLTBCPQIUCUBLRS $.
      $( [26-Apr-2006] $) $( [16-Feb-2006] $)

    $( Lemma for ~ cnlnadj .  ` G ` is a continuous linear functional. $)
    cnlnadjlem2 $p |- ( y e. H~ -> ( G e. LinFn /\ G e. ConFn ) ) $=
      ( vx vw vz cv chil wcel clf ccnf cc wf csm co cva cfv cmul caddc wceq
      wral wa csp ax-hicl lnopf ffvrni sylan ancoms r19.21aiva fopab2 sylib
      w3a lnopadd 3adant3 opreq1d ax-his2 id syl3an eqtrd 3comr 3expa hvmulclt
      sylanl2 hvaddclt cnlnadjlem1 syl adantll ax-his3 syl3an2 3expb lnopmul
      adantl opreq2d ad2antll 3eqtr4rd opreqan12d 3eqtr4d ex r19.21aivv jca
      ellnfnt sylibr cabs cno cle wbr cr wrex cnop opreq1 breq2d ralbidv
      rcla4ev normclt nmcopex axmulrcl mpan adantr eqeltrd absclt syl2an
      fveq2d bcst eqbrtrd cc0 lemul1it 3jca normge0t nmcoplb sylanc letrd recn
      mul23t mp3an1 recnd breqtrd wb lnfncont mpbird ) ALZMNZEONZEPNZYFMQERZILZ
      JLZSTZKLZUATZEUBZYJYKEUBZUCTZYMEUBZUDTZUEZKMUFZJMUFIQUFZUGYGYFYIUUBYFCLZB
      UBZYEUHTZQNZCMUFYIYFUUFCMUUCMNZYFUUFUUDMNYFUUFUUGUUDYEUIMMUUCBBFUJZUKULUM
      UNCDMQUUEEHUOUPYFUUAIJQMYFYJQNZYKMNZUGZUUAYFUUKUGZYTKMUULYMMNZUGYNBUBZYEU
      HTZYLBUBZYEUHTZYMBUBZYEUHTZUDTZYOYSYFYLMNZUUMUUOUUTUEZUUKYFUVAUUMUVBUVAUU
      MYFUVBUVAUUMYFUQZUUOUUPUURUATZYEUHTZUUTUVCUUNUVDYEUHUVAUUMUUNUVDUEYFYLYMB
      FURUSUTUUPMNUURMNZYFUVEUUTUEUVAUUMYFUUPUURYEVAMMYLBUUHUKMMYMBUUHUKZYFVBVC
      VDVEVFYJYKVGZVHUUKUUMYOUUOUEZYFUUKUUMUGYNMNZUVIUVAUUMUVJUUKYLYMVIUVHULAYN
      BCDEFGHVJVKVLUULUUMYQUUQYRUUSUDUULYJYKBUBZSTZYEUHTZYJUVKYEUHTZUCTZUUQYQYF
      UUIUUJUVMUVOUEZUUIUUJYFUVPUUIUVKMNYFUVPUUJYJUVKYEVMMMYKBUUHUKVNVEVOUUKUUQ
      UVMUEYFUUKUUPUVLYEUHYJYKBFVPUTVQUUJYQUVOUEYFUUIUUJYPUVNYJUCAYKBCDEFGHVJVR
      VSVTAYMBCDEFGHVJZWAWBUNWCWDWEIJKEWFWGZYFYHYRWHUBZYJYMWIUBZUCTZWJWKZKMUFZI
      WLWMZBWNUBZYEWIUBZUCTZWLNZUVSUWGUVTUCTZWJWKZKMUFZUWDYFUWCUWKIUWGWLYJUWGUE
      ZUWBUWJKMUWLUWAUWIUVSWJYJUWGUVTUCWOWPWQWRYFUWFWLNZUWHYEWSZUWEWLNZUWMUWHBF
      GWTZUWEUWFXAXBVKYFUWJKMUUMYFUWJUUMYFUGZUVSUWEUVTUCTZUWFUCTZUWIWJUWQUVSUUR
      WIUBZUWFUCTZUWSUWQYRQNUVSWLNUWQYRUUSQUUMYRUUSUEYFUVQXCZUVFYFUUSQNUUMUURYE
      UIUVGULXDYRXEVKUWTWLNZUWMUXAWLNUUMYFUWTUWFXAUUMUVFUXCUVGUURWSVKZUWNXFUWRW
      LNZUWMUWSWLNUUMYFUWRUWFXAUUMUVTWLNZUXEYMWSZUWOUXFUXEUWPUWEUVTXAXBVKZUWNXF
      UWQUVSUUSWHUBZUXAWJUWQYRUUSWHUXBXGUVFYFUXIUXAWJWKUUMUURYEXHUVGULXIUXCUXEU
      WMUQXJUWFWJWKZUWTUWRWJWKZUGUXAUWSWJWKUWQUWTUWRUWFXKUWQUXCUXEUWMUUMUXCYFUX
      DXCUUMUXEYFUXHXCYFUWMUUMUWNVQXLUWQUXJUXKYFUXJUUMYEXMVQUUMUXKYFYMBFGXNXCWE
      XOXPUVTQNZUWFQNZUWSUWIUEZUUMYFUWEQNUXLUXMUXNUWEUWPXQUWEUVTUWFXRXSUUMUVTUX
      GXTYFUWFUWNXTXFYAUMUNXOYFYGYHUWDYBUVRIKEYCVKYDWE $.
      $( [27-Apr-2006] $) $( [16-Feb-2006] $)

    ${
      cnlnadjlem.4 $e |- B = U. { w e. H~ | A. v e. H~ ( ( T ` v ) .ih y )
                         = ( v .ih w ) } $.
      cnlnadjlem.5 $e |- F = { <. y , u >. | ( y e. H~ /\ u = B ) } $.
      $( Lemma for ~ cnlnadj .  By ~ riesz4t , ` B ` is the unique vector
         such that ` ( T `` v ) .ih y ) = ( v .ih w ) ` for all ` v ` . $)
      cnlnadjlem3 $p |- ( y e. H~ -> B e. H~ ) $=
        ( cv chil wcel cfv csp co wceq wral crab cuni wreu clf ccnf cin wa
        cnlnadjlem2 elin sylibr riesz4t syl cnlnadjlem1 eqeq1d ralbiia reubii
        sylib reucl syl5eqel ) APZQRZCPZFSVCTUAZVEBPTUAZUBZCQUCZBQUDUEZQEVDVIBQ
        UFZVJQRVDVEJSZVGUBZCQUCZBQUFZVKVDJUGUHUIRZVOVDJUGRJUHRUJVPAFGHJKLMUKJUG
        UHULUMBCJUNUOVNVIBQVMVHCQVEQRVLVFVGAVEFGHJKLMUPUQURUSUTVIBQVAUONVB $.
        $( [28-Apr-2006] $) $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadj .  The values of auxiliary function ` F ` are
         vectors. $)
      cnlnadjlem4 $p |- ( A e. H~ -> ( F ` A ) e. H~ ) $=
        ( chil wcel cfv cv csp co wceq wral crab cuni opreq2 eqeq1d ralbidv
        rabbisdv unieqd syl5eq ax-hilex rabex uniex fvopab4 wreu reubidv clf
        ccnf cin wa cnlnadjlem2 elin sylibr riesz4t syl cnlnadjlem1 ralbiia
        reubii sylib vtoclga reucl eqeltrd ) EQRZEJSCTZGSZEUAUBZVPBTUAUBZUCZCQU
        DZBQUEZUFZQADEFWCQJATZEUCZVQWDUAUBZVSUCZCQUDZBQUEZUFWCFWEWIWBWEWHWABQWE
        WGVTCQWEWFVRVSWDEVQUAUGUHUIZUJUKOULPWBWABQUMUNUOUPVOWABQUQZWCQRWHBQUQZW
        KAEQWEWHWABQWJURWDQRZVPKSZVSUCZCQUDZBQUQZWLWMKUSUTVARZWQWMKUSRKUTRVBWRA
        GHIKLMNVCKUSUTVDVEBCKVFVGWPWHBQWOWGCQVPQRWNWFVSAVPGHIKLMNVHUHVIVJVKVLWA
        BQVMVGVN $.
        $( [27-Apr-2006] $) $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadj .  ` F ` is an adjoint of ` T ` (later, we will
         show it is unique). $)
      cnlnadjlem5 $p |- ( ( A e. H~ /\ C e. H~ ) -> ( ( T ` C ) .ih A ) =
                        ( C .ih ( F ` A ) ) ) $=
        ( vf vz chil wcel cfv csp co wceq cv wi fveq2 opreq1d opreq1 eqeq12d
        imbi2d wral ax-17 wa copab wal hbopab1 eleq2i albii 3imtr4 hbfv hbopr
        hbeq hbral opreq2 opreq2d ralbidv crab cuni cvv ax-hilex rabex uniex
        eqeltr fvopab2 mpan2 fveq1i syl5eq wb cbvralv a1i rabbii unieqi eqtr
        syl6req wreu eqeq2d reuuni2 cnlnadjlem3 eqeltrd clf ccnf cin
        cnlnadjlem2 elin sylibr riesz4t syl cnlnadjlem1 eqeq1d ralbiia reubii
        sylib sylanc mpbird vtoclgaf ra4 com12 vtoclga impcom ) GTUAETUAZGHUBZE
        UCUDZGEKUBZUCUDZUEZXLRUFZHUBZEUCUDZXRXOUCUDZUEZUGXLXQUGRGTXRGUEZYBXQXLY
        CXTXNYAXPYCXSXMEUCXRGHUHUIXRGXOUCUJUKULXLXRTUAZYBXLYBRTUMZYDYBUGXSAUFZU
        CUDZXRYFKUBZUCUDZUEZRTUMZYEASETSUFZEUAAUNZYBARTYDAUNASSXTYAYLXTUAAUNASX
        RXOUCYLXRUAAUNYLUCUAAUNASEKYLYFTUAZDUFFUEUOZADUPZUAZYQAUQYLKUAZYRAUQYOA
        DSURKYPYLQUSZYRYQAYSUTVAYMVBVCVDVEYFEUEZYJYBRTYTYGXTYIYAYFEXSUCVFYTYHXO
        XRUCYFEKUHVGUKVHYNYKYGXRBUFZUCUDZUEZRTUMZBTVIZVJZYHUEZYNYHFUUFYNYFYPUBZ
        FYHYNFVKUAUUHFUEFCUFZHUBZYFUCUDZUUIUUAUCUDZUEZCTUMZBTVIZVJZVKPUUOUUNBTV
        LVMVNVOADTFVKVPVQYFKYPQVRVSZFUUPUUFPUUOUUEUUNUUDBTUUNUUDVTUUATUAUUMUUCC
        RTUUIXRUEZUUKYGUULUUBUURUUJXSYFUCUUIXRHUHUIUUIXRUUAUCUJUKWAWBWCWDWEWFYH
        TUAUUDBTWGZYKUUGVTYNUUDYKBTYHUUAYHUEZUUCYJRTUUTUUBYIYGUUAYHXRUCVFWHVHWI
        YNYHFTUUQABCDFHIJKLMNOPQWJWKYNXRLUBZUUBUEZRTUMZBTWGZUUSYNLWLWMWNUAZUVDY
        NLWLUALWMUAUOUVEAHIJLMNOWOLWLWMWPWQBRLWRWSUVCUUDBTUVBUUCRTYDUVAYGUUBAXR
        HIJLMNOWTXAXBXCXDXEXFXGYBRTXHWSXIXJXK $.
        $( [28-Apr-2006] $) $( [18-Feb-2006] $)

      $( Lemma for ~ cnlnadj .  ` F ` is linear. $)
      cnlnadjlem6 $p |- F e. LinOp $=
        ( vx vf vz vt clo wcel chil wf cv csm co cva cfv wceq wral cc ellnopt
        cnlnadjlem3 fopab wa csp caddc his7t lnopf ffvrni adantl hvmulclt
        ad2antrr simplr syl3anc cnlnadjlem5 hvaddclt sylan ccj cmul adantll
        opreq2d his5t simpll pm3.27 cnlnadjlem4 ad2antlr 3eqtr4d adantlr
        opreq12d sylan2 eqtr4d 3eqtr3d r19.21aiva wb hial2eq2t syl syl2an
        sylanc mpbid rgen2a mpbir2an ) ITUAUBUBIUCPUDZQUDZUEUFZRUDZUGUFZIUHZWMW
        NIUHZUEUFZWPIUHZUGUFZUIZRUBUJZQUBUJPUKUJPQRIULADUBUBEIOABCDEFGHIJKLMNOU
        MUNXDPQUKUBWMUKUAZWNUBUAZUOZXCRUBXGWPUBUAZUOZSUDZWRUPUFZXJXBUPUFZUIZSUB
        UJZXCXIXMSUBXIXJUBUAZUOZXJFUHZWQUPUFZXQWOUPUFZXQWPUPUFZUQUFZXKXLXQUBUAZ
        WOUBUAZXHXRYAUIXPXQWOWPURXOYBXIUBUBXJFFKUSUTZVAXGYCXHXOWMWNVBZVCXGXHXOV
        DVEWQUBUAZXOXRXKUIXIABCDWQEXJFGHIJKLMNOVFYCXHYFXGWOWPVGYEVHZVHXPYAXJWTU
        PUFZXJXAUPUFZUQUFZXLXPXSYHXTYIUQXGXOXSYHUIXHXGXOUOZWMVIUHZXQWNUPUFZVJUF
        ZYLXJWSUPUFZVJUFZXSYHYKYMYOYLVJXFXOYMYOUIXEABCDWNEXJFGHIJKLMNOVFVKVLXEY
        BXFXSYNUIYKWMXQWNVMXEXFXOVNZXOYBXGYDVAXEXFXOVDVEXEXOWSUBUAZYHYPUIYKWMXJ
        WSVMYQXGXOVOXFYRXEXOABCDWNEFGHIJKLMNOVPZVQVEVRVSXHXOXTYIUIXGABCDWPEXJFG
        HIJKLMNOVFVKVTXOWTUBUAZXAUBUAZXLYJUIXPXJWTXAURXIXOVOXGYTXHXOXEYRYTXFWMW
        SVBYSWAZVCXHUUAXGXOABCDWPEFGHIJKLMNOVPZVQVEWBWCWDWRUBUAZXBUBUAZXNXCWEXI
        SWRXBWFXIYFUUDYGABCDWQEFGHIJKLMNOVPWGYTUUAUUEXGXHWTXAVGUUBUUCWHWIWJWDWK
        WL $.
        $( [28-Apr-2006] $) $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadj .  Helper lemma to show that ` F ` is
         continuous. $)
      cnlnadjlem7 $p |- ( A e. H~ -> ( normh ` ( F ` A ) ) <_
                        ( ( normop ` T ) x. ( normh ` A ) ) ) $=
        ( chil wcel cc0 cfv cno wceq cnop cmul co cle wbr wa pm3.27 cr mulge0t
        normclt nmcopex jctil normge0t wf lnopf nmopge0t ax-mp sylanc adantr
        eqbrtrrd wn csp cabs cc cnlnadjlem4 ffvrni syl ax-hicl mpancom absclt
        axmulrcl mpan bcst w3a lemul1it 3jca nmcoplb jca letrd c2 cexp normsqt
        recnd sqvalt cnlnadjlem5 mpdan fveq2d absidt hiidrclt hiidge0t eqtr2d
        3eqtr3rd recn mul23t mp3an1 3brtr3d clt wb lemul1t 0re leltnet biimpar
        sylan mpbird pm2.61dan ) EQRZSEJTZUATZUBZXJGUCTZEUATZUDUEZUFUGZXHXKUHSX
        JXNUFXHXKUIXHSXNUFUGZXKXLUJRZXMUJRZUHSXLUFUGZSXMUFUGZUHXPXHXLXMUKXHXRXQ
        EULZGLMUMZUNXHXTXSEUOZQQGUPXSGLUQZGURUSUNUTVAVBXHXKVCZUHZXOXJXJUDUEZXNX
        JUDUEZUFUGZXHYIYEXHXIGTZEVDUEZVETZXLXJUDUEZXMUDUEZYGYHUFXHYLYJUATZXMUDU
        EZYNXHYKVFRZYLUJRYJQRZXHYQXHXIQRZYRABCDEFGHIJKLMNOPVGZQQXIGYDVHVIZYJEVJ
        VKYKVLVIYOUJRZXRYPUJRXHYOXMVMXHYRUUBUUAYJULVIZYAUTYMUJRZXRYNUJRXHYMXMVM
        XHXJUJRZUUDXHYSUUEYTXIULZVIZXQUUEUUDYBXLXJVMVNVIZYAUTYRXHYLYPUFUGUUAYJE
        VOVKUUBUUDXRVPXTYOYMUFUGZUHYPYNUFUGXHYOYMXMVQXHUUBUUDXRUUCUUHYAVRXHXTUU
        IYCXHYSUUIYTXIGLMVSVIVTUTWAXHXJWBWCUEZXIXIVDUEZYGYLXHYSUUJUUKUBYTXIWDVI
        XHXJVFRZUUJYGUBXHXJUUGWEZXJWFVIXHYLUUKVETZUUKXHYKUUKVEXHYSYKUUKUBYTABCD
        EFXIGHIJKLMNOPWGWHWIUUKUJRZSUUKUFUGZUUNUUKUBXHUUKWJXHYSUUOYTXIWKVIXHYSU
        UPYTXIWLVIUTWMWNUULXMVFRZYNYHUBZXHXLVFRUULUUQUURXLYBWOXLXJXMWPWQUUMXHXM
        YAWEUTWRVAUUEXNUJRZUUEVPZSXJWSUGZXOYIWTYFXJXNXJXAXHUUTYEXHUUEUUSUUEUUGX
        HXRUUSYAXQXRUUSYBXLXMVMVNVIUUGVRVAYSYEUVAXHYSUVAYEUUESXJUFUGZUVAYEWTZYS
        SUJRUUEUVBUVCXBSXJXCWQUUFXIUOUTXDYTXEUTXFXG $.
        $( [28-Apr-2006] $) $( [18-Feb-2006] $)

      $( Lemma for ~ cnlnadj .  ` F ` is continuous. $)
      cnlnadjlem8 $p |- F e. ConOp $=
        ( vz vx cco wcel cv cfv cno cmul co cle wbr chil wral cr wrex cnop
        nmcopex cnlnadjlem7 rgen wceq opreq1 breq2d ralbidv rcla4ev mp2an
        cnlnadjlem6 lnopcon mpbir ) IRSPTZIUAUBUAZQTZVDUBUAZUCUDZUEUFZPUGUHZQUI
        UJZFUKUAZUISVEVLVGUCUDZUEUFZPUGUHZVKFKLULVNPUGABCDVDEFGHIJKLMNOUMUNVJVO
        QVLUIVFVLUOZVIVNPUGVPVHVMVEUEVFVLVGUCUPUQURUSUTQPIABCDEFGHIJKLMNOVAVBVC
        $.
        $( [29-Apr-2006] $) $( [17-Feb-2006] $)

      $( Lemma for ~ cnlnadj .  ` F ` provides an example showing the existence
         of a continuous linear adjoint. $)
      cnlnadjlem9 $p |- E. t e. ( LinOp i^i ConOp )
             A. x e. H~ A. z e. H~ ( ( T ` x ) .ih z ) = ( x .ih ( t ` z ) ) $=
        ( clo cco cin wcel cv cfv csp co wceq chil wral wrex elin cnlnadjlem6
        cnlnadjlem8 mpbir2an cnlnadjlem5 ancoms rgen2 fveq1 opreq2d eqeq2d
        2ralbidv rcla4ev mp2an ) LSTUAZUBZAUCZIUDCUCZUEUFZVFVGLUDZUEUFZUGZCUHUI
        AUHUIZVHVFVGGUCZUDZUEUFZUGZCUHUIAUHUIZGVDUJVELSUBLTUBLSTUKBDEFHIJKLMNOP
        QRULBDEFHIJKLMNOPQRUMUNVKACUHVGUHUBVFUHUBVKBDEFVGHVFIJKLMNOPQRUOUPUQVQV
        LGLVDVMLUGZVPVKACUHUHVRVOVJVHVRVNVIVFUEVGVMLURUSUTVAVBVC $.
        $( [29-Apr-2006] $) $( [18-Feb-2006] $)
    $}
  $}

  ${
    $d f g h t u v w x y z T $.
    cnlnadj.1 $e |- T e. LinOp $.
    cnlnadj.2 $e |- T e. ConOp $.
    $( Every continuous linear operator has an adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadj $p |- E. t e. ( LinOp i^i ConOp )
             A. x e. H~ A. y e. H~ ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) $=
      ( vz vw vv vu vf vg vh cv cfv csp co wceq chil wral crab cuni wcel wa
      copab eqid opreq2 eqeq2d ralbidv cbvrabv unieqi cnlnadjlem9 ) AGBHIJCINZD
      OGNZPQZUMKNZPQZRZISTZKSUAZUBZDLMUNSUCJNVARUDGJUEZLNZSUCMNVCDOUNPQRUDLMUEZ
      EFVDUFUTUOUMHNZPQZRZISTZHSUAUSVHKHSUPVERZURVGISVIUQVFUOUPVEUMPUGUHUIUJUKV
      BUFUL $.
      $( [29-Apr-2006] $) $( [18-Feb-2006] $)

    $( Every continuous linear operator has a unique adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadjeu $p |- E! t e. ( LinOp i^i ConOp )
             A. x e. H~ A. y e. H~ ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) $=
      ( cv cfv csp co wceq chil wral clo cco cin wreu wrex wcel wa wmo reu5
      cnlnadj wf adjmo pm3.26 wb lnopf adjsymt mpan2 eqcom 2ralbii syl5bb
      biimpa jca inss1 sseli lnopft syl sylan immoi ax-mp mpbir2an ) AGZDHBGZIJ
      ZVDVECGZHIJZKZBLMALMZCNOPZQVJCVKRVGVKSZVJTZCUAZVJCVKUBABCDEFUCLLVGUDZVDVE
      DHIJVDVGHVEIJKBLMALMZTZCUAVNABCDUEVMVQCVOVJVQVLVOVJTVOVPVOVJUFVOVJVPVOVHV
      FKZBLMALMZVPVJVOLLDUDVSVPUGDEUHABVGDUIUJVIVRABLLVFVHUKULUMUNUOVLVGNSVOVKN
      VGNOUPUQVGURUSUTVAVBVC $.
      $( [29-Apr-2006] $) $( [18-Feb-2006] $)
  $}

  ${
    $d t x y T $.
    $( Every continuous linear operator has a unique adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadjeut $p |- ( T e. ( LinOp i^i ConOp ) -> E! t e. ( LinOp i^i ConOp )
           A. x e. H~ A. y e. H~ ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) ) $=
      ( clo cco cin wcel cv cfv csp co wceq chil wral wreu c0h cxp cif fveq1
      opreq1d eqeq1d 2ralbidv reubidv inss1 elin 0lnopOLD 0cnop mpbir2an
      elimel sselii inss2 cnlnadjeu dedth ) DEFGZHZAIZDJZBIZKLZUQUSCIJKLZMZBNOA
      NOZCUOPUQUPDNQRZSZJZUSKLZVAMZBNOANOZCUOPDVDDVEMZVCVICUOVJVBVHABNNVJUTVGVA
      VJURVFUSKUQDVETUAUBUCUDABCVEUOEVEEFUEDVDUOVDUOHVDEHVDFHVDEFUFUGUHUIUJZUKU
      OFVEEFULVKUKUMUN $.
      $( [29-Apr-2006] $) $( [19-Feb-2006] $)

    $( Every continuous linear operator has an adjoint.  Theorem 3.10 of
       [Beran] p. 104. $)
    cnlnadjt $p |- ( T e. ( LinOp i^i ConOp ) -> E. t e. ( LinOp i^i ConOp )
           A. x e. H~ A. y e. H~ ( ( T ` x ) .ih y ) = ( x .ih ( t ` y ) ) ) $=
      ( clo cco cin wcel cv cfv csp co wceq chil wral wreu wrex cnlnadjeut
      reurex syl ) DEFGZHAIZDJBIZKLUBUCCIJKLMBNOANOZCUAPUDCUAQABCDRUDCUAST $.
      $( [29-Apr-2006] $) $( [18-Feb-2006] $)
  $}

  ${
    $d t u v x y z $.
    $( Every continuous linear Hilbert space operator has an adjoint. $)
    cnlnssadj $p |- ( LinOp i^i ConOp ) (_ dom adj $=
      ( vy vt vx vz vu vv clo cco cin cado cdm cv wcel cop wex cfv csp co wceq
      chil wral wa wrex cnlnadjt df-rex sylib wf w3a inss1 sseli lnopft syl
      a1d wi a1i adantrd wb adjsymt syl2an ancoms eqcom biimp r19.20si syl5bi
      ex imp3a 3jcad copab dfadj2 eleq2i visset feq1 fveq1 opreq2d eqeq1d
      2ralbidv 3anbi13d opreq1d eqeq2d 3anbi23d opelopab bitr2 syl6ib 19.22dv
      mpd eldm2 sylibr ssriv ) AGHIZJKZALZWIMZWKBLZNZJMZBOZWKWJMWLWMWIMZCLZWKPD
      LZQRZWRWSWMPQRZSZDTUAZCTUAZUBZBOZWPWLXDBWIUCXFCDBWKUDXDBWIUEUFWLXEWOBWLXE
      TTWKUGZTTWMUGZWRWSWKPZQRZWRWMPZWSQRZSZDTUACTUAZUHZWOWLXEXGXHXNWLXGXEWLWKG
      MXGWIGWKGHUIZUJWKUKULZUMWLWQXHXDWQXHUNWLWQWMGMXHWIGWMXPUJWMUKULZUOUPWLWQX
      DXNWLWQXDXNUNWLWQUBXAWTSZDTUAZCTUAZXNXDWQWLYAXNUQZXHXGYBWQWLCDWMWKURXRXQU
      SUTXCXTCTXBXSDTXBXSWTXAVAVBVCVCVDVEVFVGWOWNTTELZUGZTTFLZUGZWRWSYCPZQRZWRY
      EPZWSQRZSZDTUACTUAZUHZEFVHZMXOJYNWNCDFEVIVJYMXGYFXJYJSZDTUACTUAZUHXOEFWKW
      MAVKZBVKYCWKSZYDXGYLYPYFTTYCWKVLYRYKYOCDTTYRYHXJYJYRYGXIWRQWSYCWKVMVNVOVP
      VQYEWMSZYFXHYPXNXGTTYEWMVLYSYOXMCDTTYSYJXLXJYSYIXKWSQWRYEWMVMVRVSVPVTWAWB
      WCWDWEBWKJYQWFWGWH $.
      $( [24-May-2006] $) $( [18-Feb-2006] $)
  $}


  $( Every bounded linear Hilbert space operator has an adjoint. $)
  bdopssadj $p |- BndLinOp (_ dom adj $=
    ( cbo clo cco cin cado cdm lncnbd cnlnssadj eqsstr3 ) ABCDEFGHI $.
    $( [4-Jun-2006] $) $( [19-Feb-2006] $)

  $( Every bounded linear Hilbert space operator has an adjoint. $)
  bdopadjt $p |- ( T e. BndLinOp -> T e. dom adj ) $=
    ( cbo cado cdm bdopssadj sseli ) BCDAEF $.
    $( [10-Jun-2006] $) $( [22-Feb-2006] $)

  ${
    $d t w x y z T $.
    $( The adjoint of a bounded linear operator is a bounded linear
       operator. $)
    adjbdlnt $p |- ( T e. BndLinOp -> ( adj ` T ) e. BndLinOp ) $=
      ( vx vy vt cbo wcel cado cfv cv csp co wceq chil wral crab cuni cm wf wa
      cab df-rab ax-hilex elmap anbi1i abbii eqtr unieqi a1i wrex wmo wss
      bdopft sylibr ssriv mouniss mp3an1 clo cco cin cnlnadjt lncnopbd wb
      lncnbd rexeq1 ax-mp 3imtr3 adjmo ax-17 adjsymt sylan pm5.32da eqcom
      2ralbii anbi12i syl6rbbr mobid mpbiri sylanc adjval2t syl 3eqtr4rd wreu
      cnlnadjeut reueq1 reucl eqeltrd ) AEFZAGHZBIZAHCIZJKZWIWJDIZHJKZLZCMNBMNZ
      DEOPZEWGWODMMQKZOZPZMMWLRZWOSZDTZPZWPWHWSXCLWGWRXBWRWLWQFZWOSZDTXBWODWQUA
      XEXADXDWTWOMMWLUBUBUCZUDUEUFUGUHWODEUIZXEDUJZWPWSLZWGEWQUKXGXHXIDEWQWLEFW
      TXDWLULXFUMUNWODEWQUOUPAUQURUSZFZWODXJUIZWGXGBCDAUTAVAZXJELZXLXGVBVCWODXJ
      EVDVEVFWGXHWTWIWJAHJKWIWLHWJJKLCMNBMNZSZDUJBCDAVGWGXEXPDWGDVHWGXPWTWMWKLZ
      CMNBMNZSXEWGWTXOXRMMARZWTXOXRVBWGBCAWLVIAULZVJVKXDWTWOXRXFWNXQBCMMWKWMVLV
      MVNVOVPVQVRWGXSWHXCLXTBCDAVSVTWAWGWODEWBZWPEFXKWODXJWBZWGYABCDAWCXMXNYBYA
      VBVCWODXJEWDVEVFWODEWEVTWF $.
      $( [29-Apr-2006] $) $( [19-Feb-2006] $)

    $( An operator is bounded and linear iff its adjoint is. $)
    adjbdlnb $p |- ( T e. BndLinOp <-> ( adj ` T ) e. BndLinOp ) $=
      ( cbo wcel cado cfv adjbdlnt cdm bdopadjt dmadjrnb sylibr wa ccom cid
      cres wceq ccnv cnvadj coeq1i wf1o adj1o f1ococnv1 ax-mp eqtr3 fveq1i a1i
      wfun funadj fvco mp3an12 adantr fvresi 3eqtr3rd adantl eqeltrd mpancom
      impbi ) ABCZADEZBCZAFADGZCZUSUQUSURUTCVAURHAIJVAUSKZAURDEZBVBADDLZEZAMUTN
      ZEZVCAVEVGOVBAVDVFDPZDLZVDVFVHDDQRUTUTDSVIVFOTUTUTDUAUBUCUDUEVAVEVCOZUSDU
      FZVKVAVJUGUGADDUHUIUJVAVGAOUSUTAUKUJULUSVCBCVAURFUMUNUOUP $.
      $( [7-Jun-2006] $) $( [19-Feb-2006] $)

    $( The mapping of adjoints of bounded linear operators is
       one-to-one onto. $)
    adjbd1o $p |- ( adj |` BndLinOp ) : BndLinOp -1-1-onto-> BndLinOp $=
      ( vy vx cbo cado cima cres wf1o cdm wf1 wss adj1o f1of1 ax-mp bdopssadj
      f1ores mp2an wceq wb cv wcel wbr wrex cfv visset elima bdopadjt wfn
      f1ofn fnbrfvb mpan syl rexbiia eleq1 adjbdlnb syl5bb biimpcd r19.23aiv
      fveq2 eqeq1d rcla4ev adjbdlnt adjadjt sylanc impbi 3bitr2 eqriv f1oeq3
      mpbi ) CDCEZDCFZGZCCVJGZDHZVMDIZCVMJVKVMVMDGZVNKVMVMDLMNVMVMCDOPVICQVKVLR
      AVICASZVITBSZVPDUAZBCUBVQDUCZVPQZBCUBZVPCTZBVPDCAUDZUEVTVRBCVQCTZVQVMTZVT
      VRRZVQUFDVMUGZWEWFVOWGKVMVMDUHMVMVQVPDWCUIUJUKULWAWBVTWBBCVTWDWBVTVSCTWBW
      DVSVPCUMVQUNUOUPUQVPDUCZCTWHDUCZVPQZWAWBVTWJBWHCVQWHQVSWIVPVQWHDURUSUTVPV
      AWBVPVMTWJVPUFVPVBUKVCVDVEVFVICCVJVGMVH $.
      $( [20-Oct-2006] $) $( [19-Feb-2006] $)

    $( The adjoint of an operator is linear.  Proposition 1 of
       [AkhiezerGlazman] p. 80. $)
    adjlnopt $p |- ( T e. dom adj -> ( adj ` T ) e. LinOp ) $=
      ( vx vy vz vw cado cdm wcel chil cfv wf cv csm co cva wceq wral cc wa
      clo dmadjrnt dmadjopt syl csp w3a caddc his7t ffvrn sylan 3adant3
      hvmulclt adantr 3ad2ant3 pm3.27 syl3anc adj2t hvaddclt syl3an3 ccj cmul
      3adant3l opreq2d his5t pm3.26 3simp2 adjclt adantrl 3adant2 3eqtr4d
      3adant3r opreq12d sylan2 an1s adantrr eqtr4d 3eqtr3d 3com23 3expa
      r19.21aiva wb hial2eq2t syl2an anandis sylanc mpbid exp32 r19.21adv
      r19.21aivv jca ellnopt sylibr ) AFGZHZIIAFJZKZBLZCLZMNZDLZONZWNJZWPWQWNJZ
      MNZWSWNJZONZPZDIQZCIQBRQZSWNTHWMWOXHWMWNWLHWOAUAWNUBUCWMXGBCRIWMWPRHZWQIH
      ZSZXFDIWMXKWSIHZXFWMXKXLSZSZELZXAUDNZXOXEUDNZPZEIQZXFXNXREIWMXMXOIHZXRWMX
      TXMXRWMXTXMUEZXOAJZWTUDNZYBWRUDNZYBWSUDNZUFNZXPXQYBIHZWRIHZXLYCYFPYAYBWRW
      SUGWMXTYGXMIIAKXTYGWMIIXOAUHAUBUIZUJXMWMYHXTXKYHXLWPWQUKZULUMXMWMXLXTXKXL
      UNUMUOWMXTWTIHZYCXPPXMXOWTAUPYHXLYKXKWRWSUQYJUIZURYAYFXOXCUDNZXOXDUDNZUFN
      ZXQYAYDYMYEYNUFWMXTXKYDYMPXLWMXTXKUEZWPUSJZYBWQUDNZUTNZYQXOXBUDNZUTNZYDYM
      YPYRYTYQUTWMXTXJYRYTPXIXOWQAUPVAVBXIYGXJYDYSPYPWPYBWQVCXKWMXIXTXIXJVDUMZW
      MXTYGXKYIUJXKWMXJXTXIXJUNUMUOXIXTXBIHZYMUUAPYPWPXOXBVCUUBWMXTXKVEWMXKUUCX
      TWMXJUUCXIWQAVFZVGVHUOVIVJWMXTXLYEYNPXKXOWSAUPVAVKXTXCIHZXDIHZXQYOPYAXOXC
      XDUGWMXTXMVEWMXMUUEXTWMXKUUEXLXIWMXJUUEXIUUCUUEWMXJSWPXBUKUUDVLVMZVNVHWMX
      MUUFXTWMXLUUFXKWSAVFZVGVHUOVOVPVQVRVSXAIHZXEIHZXSXFVTXNEXAXEWAWMYKUUIXMWT
      AVFYLVLWMXKXLUUJUUEUUFUUJWMXKSWMXLSXCXDUQUUGUUHWBWCWDWEWFWGWHWIBCDWNWJWK
      $.
      $( [28-Jun-2006] $) $( [17-Jun-2006] $)

    $( Every operator with an adjoint is linear. $)
    adjsslnop $p |- dom adj (_ LinOp $=
      ( vt cado cdm clo cv wcel cfv adjadjt dmadjrnt adjlnopt syl eqeltrrd
      ssriv ) ABCZDAEZNFZOBGZBGZODOHPQNFRDFOIQJKLM $.
      $( [11-Nov-2006] $) $( [17-Jun-2006] $)
  $}

  ${
    $d f g h u v w z A $.  $d f g h t u v w x y z T $.
    nmopadjle.1 $e |- T e. BndLinOp $.
    $( Property of the norm of an adjoint.  Part of proof of Theorem 3.10 of
       [Beran] p. 104. $)
    nmopadjle $p |- ( A e. H~ -> ( normh ` ( ( adj ` T ) ` A ) ) <_
                        ( ( normop ` T ) x. ( normh ` A ) ) ) $=
      ( vz vu vv vf vw vg vh chil wcel cado cfv cno cv csp co wceq wral crab
      cuni wa copab cnop cmul cle cdm cbo bdopssadj sselii adjvalvalt mpan
      opreq2 eqeq1d ralbidv rabbisdv unieqd eqid ax-hilex rabex uniex fvopab4
      eqtr4d fveq2d clo cco cin inss1 lncnbd eleqtrr inss2 eqeq2d cbvrabv
      unieqi cnlnadjlem7 eqbrtrd ) AKLZABMNNZONADPZKLEPFPZBNZVTQRZWAGPZQRZSZFKT
      ZGKUAZUBZSUCDEUDZNZONBUENAONUFRUGVRVSWKOVRVSWBAQRZWESZFKTZGKUAZUBZWKBMUHZ
      LVRVSWPSUIWQBUJCUKFGABULUMDEAWIWPKWJVTASZWHWOWRWGWNGKWRWFWMFKWRWCWLWEVTAW
      BQUNUOUPUQURWJUSZWOWNGKUTVAVBVCVDVEDHFEAWIBIJWJIPZKLJPWTBNVTQRSUCIJUDZVFV
      GVHZVFBVFVGVIBUIXBCVJVKZUKXBVGBVFVGVLXCUKXAUSWHWCWAHPZQRZSZFKTZHKUAWGXGGH
      KWDXDSZWFXFFKXHWEXEWCWDXDWAQUNVMUPVNVOWSVPVQ $.
      $( [6-Jun-2006] $) $( [22-Feb-2006] $)

    $( Lemma for ~ nmopadj . $)
    nmopadjlem $p |- ( normop ` ( adj ` T ) ) <_ ( normop ` T ) $=
      ( vy chil cado cfv wf cnop cxr wcel cv cno c1 cle wbr wi wral cbo
      adjbdlnt ax-mp bdopft nmopxrt wa cmul co cr ffvrni normclt syl adantr
      nmopret axmulrcl mpan 1re remulcl a1i nmopadjle w3a cc0 lemul2it 3jca
      pm3.27 nmopge0t jctil sylanc letrd recn mulid1 syl6breq ex rgen
      nmopleubt mp3an ) DDAEFZGZAHFZIJZCKZLFZMNOZVRVNFZLFZVPNOZPZCDQVNHFVPNOVNR
      JZVOARJZWEBASTVNUATZDDAGZVQWFWHBAUATZAUBTWDCDVRDJZVTWCWJVTUCZWBVPMUDUEZVP
      NWKWBVPVSUDUEZWLWJWBUFJZVTWJWADJWNDDVRVNWGUGWAUHUIUJWJWMUFJZVTWJVSUFJZWOV
      RUHZVPUFJZWPWOWFWRBAUKTZVPVSULUMUIUJWLUFJWKVPMWSUNUOUPWJWBWMNOVTVRABUQUJW
      PMUFJZWRURUSVPNOZVTUCWMWLNOWKVSMVPUTWKWPWTWRWJWPVTWQUJWTWKUNUPWRWKWSUPVAW
      KVTXAWJVTVBWHXAWIAVCTVDVEVFVPVPWSVGVHVIVJVKCVPVNVLVM $.
      $( [22-Feb-2006] $) $( [22-Feb-2006] $)

    $( Property of the norm of an adjoint.  Theorem 3.11(v) of [Beran]
       p. 106. $)
    nmopadj $p |- ( normop ` ( adj ` T ) ) = ( normop ` T ) $=
      ( cado cfv cnop wceq cle wbr cbo wcel cr adjbdlnt ax-mp nmopret letri3
      nmopadjlem cdm bdopadjt adjadjt fveq2i eqbrtrr mpbir2an ) ACDZEDZAEDZFUDU
      EGHUEUDGHUDUEUCIJZUDKJAIJZUFBALMZUCNMUGUEKJBANMOABPUCCDZEDUEUDGUIAEACQJZU
      IAFUGUJBARMASMTUCUHPUAUB $.
      $( [11-Jun-2006] $) $( [22-Feb-2006] $)
  $}

  $( An operator is zero iff its adjoint is zero.  Theorem 3.11(i) of [Beran]
     p. 106. $)
  adjeq0 $p |- ( T = ( H~ X. 0H ) <-> ( adj ` T ) = ( H~ X. 0H ) ) $=
    ( chil c0h cxp wceq cado cfv fveq2 adj0 syl6eq cdm wcel cbo bdopssadj
    0bdop sselii eleq1 mpbiri dmadjrnb sylibr adjadjt syl a1i 3eqtr3d impbi )
    ABCDZEZAFGZUFEZUGUHUFFGZUFAUFFHIJUIUHFGZUJAUFUHUFFHUIAFKZLZUKAEUIUHULLZUMUI
    UNUFULLMULUFNOPUHUFULQRASTAUAUBUJUFEUIIUCUDUE $.
    $( [31-Oct-2006] $) $( [20-Feb-2006] $)

  ${
    $d x y A $.  $d x y T $.
    $( The adjoint of the scalar product of an operator.  Theorem 3.11(ii) of
       [Beran] p. 106. $)
    adjmult $p |- ( ( A e. CC /\ T e. dom adj ) ->
                 ( adj ` ( A .op T ) ) = ( ( * ` A ) .op ( adj ` T ) ) ) $=
      ( vx vy chil chot co wf ccj cfv cado cv csp wceq wral cc wcel cdm wa
      adjeqt homulclt dmadjopt sylan2 cjclt dmadjrnt syl syl2an csm cmul
      adj2tOLD 3expb adantll opreq2d his52t simpll simprl adjclt ad2ant2l
      syl3anc wi ax-his3 ffvrn sylan syl3an2 3exp exp3a imp43 3eqtr4rd homvalt
      3expa adantrr opreq1d id syl3an adantrl 3eqtr4d ex r19.21aivv ) EEABFGZHZ
      EEAIJZBKJZFGZHZCLZVSJZDLZMGZWEWGWCJZMGZNZDEOCEOVSKJWCNAPQZBKRZQZSZCDWCVST
      WLEEBHZVTWNABUABUBZUCWAPQZEEWBHZWDWLWNWAWBUAAUDZWNWBWMQWSBUEWBUBUFZUGWOWK
      CDEEWOWEEQZWGEQZSZWKWOXDSZAWEBJZUHGZWGMGZWEWAWGWBJZUHGZMGZWHWJXEAWEXIMGZU
      IGZAXFWGMGZUIGZXKXHXEXLXNAUIWNXDXLXNNZWLWNXBXCXPWEWGBUJUKULUMWLXBXIEQZXKX
      MNXEAWEXIUNWLWNXDUOWOXBXCUPWNXCXQWLXBWGBUQURUSWLWNXBXCXHXONZWLWNXBXCXRUTW
      LWNXBSZXCXRWLXFEQZXCXRXSAXFWGVAWPXBXTWNEEWEBVBWQVCVDVEVFVGVHXEWFXGWGMWOXB
      WFXGNZXCWLWNXBYAWLWPXBYAWNAWEBVIWQVDVJVKVLXEWIXJWEMWOXCWIXJNZXBWLWNXCYBWR
      WSXCYBWLWNXCWAWGWBVIWTXAXCVMVNVJVOUMVPVQVRUS $.
      $( [5-Nov-2006] $) $( [21-Feb-2006] $)
  $}

  ${
    $d x y S $.  $d x y T $.
    $( The adjoint of the sum of two operators.  Theorem 3.11(iii) of [Beran]
       p. 106. $)
    adjaddt $p |- ( ( S e. dom adj /\ T e. dom adj ) ->
                 ( adj ` ( S +op T ) ) = ( ( adj ` S ) +op ( adj ` T ) ) ) $=
      ( vx vy chil chos co wf cado cfv cv csp wceq wral cdm wcel wa adjeqt
      hoaddclt dmadjopt syl2an dmadjrnt syl cva caddc ax-his2 ffvrn sylan
      ad2ant2r adantrr adantll simprr syl3anc adj2tOLD 3expb adantlr opreq12d
      eqtr4d hosvaltOLD anim12i opreq1d his7t simprl adjclt adantrl ad2ant2l
      3eqtr4d opreq2d ex r19.21aivv ) EEABFGZHZEEAIJZBIJZFGZHZCKZVKJZDKZLGZVQVS
      VOJZLGZMZDENCENVKIJVOMAIOZPZBWDPZQZCDVOVKREEAHZEEBHZVLWEWFABSATZBTZUAEEVM
      HZEEVNHZVPWEWFVMVNSWEVMWDPWLAUBVMTUCZWFVNWDPWMBUBVNTUCZUAWGWCCDEEWGVQEPZV
      SEPZQZWCWGWRQZVTVQVSVMJZVSVNJZUDGZLGZWBWSVQAJZVQBJZUDGZVSLGZVQWTLGZVQXALG
      ZUEGZVTXCWSXGXDVSLGZXEVSLGZUEGZXJXDEPZXEEPZWQXGXMMWSXDXEVSUFWEWPXNWFWQWHW
      PXNWEEEVQAUGWJUHUIWFWRXOWEWFWPXOWQWIWPXOWFEEVQBUGWKUHUJUKWGWPWQULUMWSXHXK
      XIXLUEWEWRXHXKMZWFWEWPWQXPVQVSAUNUOUPWFWRXIXLMZWEWFWPWQXQVQVSBUNUOUKUQURW
      SVRXFVSLWGWPVRXFMZWQWHWIQWPXRWGVQABUSWEWHWFWIWJWKUTUHUJVAWPWTEPZXAEPZXCXJ
      MWSVQWTXAVBWGWPWQVCWEWRXSWFWEWQXSWPVSAVDVEUPWFWQXTWEWPVSBVDVFUMVGWSWAXBVQ
      LWGWQWAXBMZWPWLWMQWQYAWGVSVMVNUSWEWLWFWMWNWOUTUHVEVHURVIVJUM $.
      $( [15-Feb-2006] $) $( [22-Feb-2006] $)
  $}

  ${
    $d x y z S $.  $d x y z T $.
    nmoptri.1 $e |- S e. BndLinOp $.
    nmoptri.2 $e |- T e. BndLinOp $.
    $( Triangle inequality for the norms of bounded linear operators. $)
    nmoptri $p |- ( normop ` ( S +op T ) ) <_
                  ( ( normop ` S ) + ( normop ` T ) ) $=
      ( vx chil chos co wf cnop cfv caddc cxr wcel cv cno c1 cle wbr wi wral
      cbo bdopft ax-mp hoaddcl cr nmopret readdcl rexrt wa hoscl normclt syl
      adantr axaddrcl ffvrni sylanc a1i cva wceq hosvaltOLD mpanl12 fveq2d
      norm-iit eqbrtrd nmoplbt mp3an1 pm3.2i le2addt mpan2 mp2and letrd ex
      rgen nmopleubt mp3an ) FFABGHZIAJKZBJKZLHZMNZEOZPKQRSZWBVQKZPKZVTRSZTZEFU
      AVQJKVTRSABAUBNZFFAIZCAUCUDZBUBNZFFBIZDBUCUDZUEVTUFNZWAVRVSWHVRUFNZCAUGUD
      ZWKVSUFNZDBUGUDZUHZVTUIUDWGEFWBFNZWCWFWTWCUJZWEWBAKZPKZWBBKZPKZLHZVTWTWEU
      FNZWCWTWDFNXGWBABWJWMUKWDULUMUNWTXFUFNZWCXCUFNZXEUFNZXHWTXCXEUOWTXBFNZXIF
      FWBAWJUPZXBULUMZWTXDFNZXJFFWBBWMUPZXDULUMZUQUNWNXAWSURWTWEXFRSWCWTWEXBXDU
      SHZPKZXFRWTWDXQPWIWLWTWDXQUTWJWMWBABVAVBVCXKXNXRXFRSWTXBXDVDXLXOUQVEUNXAX
      CVRRSZXEVSRSZXFVTRSZWIWTWCXSWJWBAVFVGWLWTWCXTWMWBBVFVGWTXSXTUJYATZWCXIXJY
      BWTXIXJUJWOWQUJYBWOWQWPWRVHXCXEVRVSVIVJXMXPUQUNVKVLVMVNEVTVQVOVP $.
      $( [24-Mar-2006] $) $( [10-Mar-2006] $)

    $( Upper bound for the norm of the composition of two bounded linear
       operators. $)
    nmopco $p |- ( normop ` ( S o. T ) ) <_
                   ( ( normop ` S ) x. ( normop ` T ) ) $=
      ( vx chil ccom wf cnop cfv cmul co cxr wcel cv cno c1 cle wbr wi wral
      cbo clo bdoplnt ax-mp lnopco lnopf cr nmopret remulcl rexrt cc0 wceq wa
      0re leid a1i c0h cxp fveq1 fveq2d c0v ho0valtOLD norm0 syl6eq sylan9eq
      lnopco0 nmlnop0OLD sylib sylan opreq2 recn mul01 adantr 3brtr4d adantrr
      wne wn cdiv csm cabs cc norm-iiit recclz ffvrni syl2an lnopmul bdopft
      hoco opreq2d adantl eqtr4d divrec2t mp3an2 normclt syl recnd ancoms
      absidt rerecclz clt ltlet mpan wb nmopgt0t recgt0 sylbi sylc sylanc
      opreq1d 3eqtr4rd nmoplbt mp3an1 hvmulclt 3eqtr4d mulid2 syl6breqr 1re
      ledivmul2t mp3anl2 mpanl2 biimp mpbird eqbrtrd w3a 3jca ad2antrl mpbid
      df-ne sylanbr pm2.61ian ex rgen nmopleubt mp3an ) FFABGZHAIJZBIJZKLZMNZEO
      ZPJQRSZUUAYPJZPJZYSRSZTZEFUAYPIJZYSRSYPABAUBNZAUCNCAUDUEZBUBNZBUCNDBUDUEZ
      UFZUGZYSUHNYTYQYRUUHYQUHNZCAUIUEZUUJYRUHNZDBUIUEZUJYSUKUEUUFEFUUAFNZUUBUU
      EYRULUMZUURUUBUNZUUEUUSUURUUEUUBUUSUURUNZULULUUDYSRULULRSUVAULUOUPUQYPFUR
      USZUMZUURUUDULUMUUSUVCUURUUDUUAUVBJZPJZULUVCUUCUVDPUUAYPUVBUTVAUURUVEVBPJ
      ULUURUVDVBPUUAVCVAVDVEVFUUSUUGULUMUVCABUUIUUKVGYPUULVHVIVJUUSYSULUMUURUUS
      YSYQULKLULYRULYQKVKYQYQUUOVLVMVEVNVOVPYRULVQZUUTUUEUUSVRUVFUUTUNZUUDYRVSL
      ZYQRSZUUEUVGUVHQYRVSLZUUABJZVTLZAJZPJZYQRUVFUURUVHUVNUMUUBUVFUURUNZUVJUUC
      VTLZPJZUVJWAJZUUDKLZUVNUVHUVJWBNZUUCFNZUVQUVSUMUVFUURUVJUUCWCYRYRUUQVLZWD
      ZFFUUAYPUUMWEZWFUVOUVMUVPPUVOUVMUVJUVKAJZVTLZUVPUVTUVKFNZUVMUWFUMUVFUURUV
      JUVKAUUIWGUWCFFUUABUUJFFBHZDBWHUEZWEZWFUURUVPUWFUMUVFUURUUCUWEUVJVTUUAABU
      UHFFAHZCAWHUEZUWIWIWJWKWLVAUVOUVHUVJUUDKLZUVSUURUVFUVHUWMUMZUUDWBNZUVFUWN
      UURUWOYRWBNZUVFUWNUWBUUDYRWMWNUURUUDUURUWAUUDUHNZUWDUUCWOWPZWQVJWRUVOUVRU
      VJUUDKUVFUVRUVJUMZUURUVJUHNZULUVJRSZUWSUVFUVJWSYRUUQWTZUWTULUVJXASZUXAUVF
      ULUHNUWTUXCUXATUOULUVJXBXCUXBUVFULYRXASZUXCUWHUVFUXDXDUWIBXEUEZYRUUQXFXGX
      HXIVNZXJWLXKVPUVLFNZUVLPJZQRSZUVNYQRSZUVGUWKUXGUXIUXJUWLUVLAXLXMUVFUURUXG
      UUBUVTUWGUXGUVFUURUVJUVKXNUWCUWJWFVPUVGUXHUVKPJZYRVSLZQRUVFUURUXHUXLUMUUB
      UVOUVRUXKKLZUVJUXKKLZUXHUXLUVOUVRUVJUXKKUXFXJUVTUWGUXHUXMUMUVFUURUVJUVKWC
      UWCUWJWFUURUVFUXLUXNUMZUXKWBNZUVFUXOUURUXPUWPUVFUXOUWBUXKYRWMWNUURUXKUURU
      WGUXKUHNZUWJUVKWOWPZWQVJWRXOVPUVGUXLQRSZUXKQYRKLZRSZUUTUYAUVFUUTUXKYRUXTR
      UWHUURUUBUXKYRRSUWIUUABXLXMYRUWBXPXQWKUVFUURUXSUYAXDZUUBUURUVFUYBUXQUXDUY
      BUURUVFUXQQUHNZUXDUYBXRUXQUUPUYCUXDUYBUUQUXKYRQXSXTYAUXRUVFUXDUXEYBZWFWRV
      PYCYDXIYDUWQUUPUUNYEZUXDUVIUUEXDUVGUUDYRYQXSUURUYEUVFUUBUURUWQUUPUUNUWRUU
      PUURUUQUQUUNUURUUOUQYFYGUVFUXDUUTUYDVNXIYHYRULYIYJYKYLYMEYSYPYNYO $.
      $( [8-May-2006] $) $( [10-Mar-2006] $)

    $( The sum of two bounded linear operators is a bounded linear operator. $)
    bdophs $p |- ( S +op T ) e. BndLinOp $=
      ( chos co cbo wcel clo cnop cfv cr elbdop2t bdoplnt ax-mp lnophs cxr
      caddc wa cmnf clt wbr cle chil wf bdopft hoaddcl nmopxrt nmopret readdcl
      pm3.2i nmopgtmnf nmoptri xrret mp2an mpbir2an ) ABEFZGHUQIHUQJKZLHZUQMABA
      GHZAIHCANOBGHZBIHDBNOPURQHZAJKZBJKZRFZLHZSTURUAUBZURVEUCUBZSUSVBVFUDUDUQU
      EZVBABUTUDUDAUECAUFOVAUDUDBUEDBUFOUGZUQUHOVCVDUTVCLHCAUIOVAVDLHDBUIOUJUKV
      GVHVIVGVJUQULOABCDUMUKURVEUNUOUP $.
      $( [10-May-2006] $) $( [9-Mar-2006] $)

    $( The difference between two bounded linear operators is bounded. $)
    bdophd $p |- ( S -op T ) e. BndLinOp $=
      ( chod co c1 cneg chot chos cbo wcel chil wf bdopft ax-mp honegsubOLD cc
      ax1cn negcl bdophm bdophs eqeltr ) ABEFAGHZBIFZJFKABAKLMMANCAOPBKLMMBNDBO
      PQAUECUDRLUEKLGSTUDBDUAPUBUC $.
      $( [10-May-2006] $) $( [10-Mar-2006] $)

    $( The composition of two bounded linear operators is bounded. $)
    bdopco $p |- ( S o. T ) e. BndLinOp $=
      ( ccom cbo wcel clo cnop cfv cr elbdop2t bdoplnt ax-mp lnopco cxr cmul
      co wa cmnf clt wbr cle chil wf lnopf hocof nmopxrt nmopret remulcl
      pm3.2i nmopgtmnf nmopco xrret mp2an mpbir2an ) ABEZFGUQHGUQIJZKGZUQLABAFG
      ZAHGCAMNZBFGZBHGDBMNZOURPGZAIJZBIJZQRZKGZSTURUAUBZURVGUCUBZSUSVDVHUDUDUQU
      EZVDABAVAUFBVCUFUGZUQUHNVEVFUTVEKGCAUINVBVFKGDBUINUJUKVIVJVKVIVLUQULNABCD
      UMUKURVGUNUOUP $.
      $( [10-May-2006] $) $( [9-Mar-2006] $)

    $( Triangle-type inequality for the norms of bounded linear operators. $)
    nmoptri2 $p |- ( ( normop ` S ) - ( normop ` T ) ) <_
                  ( normop ` ( S +op T ) ) $=
      ( cnop cfv cmin co chos cle wbr caddc c1 cneg chot bdophs cc wcel cbo
      ax1cn negcl bdophm ax-mp nmoptri chod chil wf bdopft hoaddcl honegsubOLD
      hopncan eqtr3 fveq2i nmopneg opreq2i 3brtr3 cr nmopret lesubadd mpbir )
      AEFZBEFZGHABIHZEFZJKVAVDVBLHZJKVCMNZBOHZIHZEFVDVGEFZLHVAVEJVCVGABCDPZVFQR
      VGSRMTUAVFBDUBUCUDVHAEVCBUEHVHAVCBABASRZUFUFAUGCAUHUCZBSRZUFUFBUGDBUHUCZU
      IVNUJABVLVNUKULUMVIVBVDLBVNUNUOUPVAVBVDVKVAUQRCAURUCVMVBUQRDBURUCVCSRVDUQ
      RVJVCURUCUSUT $.
      $( [26-Nov-2006] $) $( [10-Mar-2006] $)

    $( The adjoint of a composition of bounded linear operators.  Theorem
       3.11(viii) of [Beran] p. 106. $)
    adjco $p |- ( adj ` ( S o. T ) ) = ( ( adj ` T ) o. ( adj ` S ) ) $=
      ( vx vy cv ccom cado cfv csp co wceq chil wral wcel wa cbo wf adjbdlnt
      ax-mp bdopft hoco opreq2d adantl opreq1d adantr cdm bdopadjt adj2t
      mp3an1 ffvrni sylan sylan2 3eqtrd bdopco 3eqtr2rd rgen2 wb hocof hoeq2t
      mp2an mpbi ) EGZFGZABHZIJZJKLZVDVEBIJZAIJZHZJZKLZMZFNOENOZVGVKMZVNEFNVDNP
      ZVENPZQZVMVDVEVJJZVIJZKLZVDVFJZVEKLZVHVRVMWBMVQVRVLWAVDKVEVIVJVIRPZNNVISB
      RPZWEDBTUAVIUBUAZVJRPZNNVJSARPZWHCATUAVJUBUAZUCUDUEVSWDVDBJZAJZVEKLZWKVTK
      LZWBVQWDWMMVRVQWCWLVEKVDABWINNASCAUBUAWFNNBSDBUBUAZUCUFUGWKNPZVRWMWNMZVQA
      IUHZPZWPVRWQWIWSCAUIUAWKVEAUJUKNNVDBWOULUMVQVTNPZWNWBMZVRBWRPZVQWTXAWFXBD
      BUIUAVDVTBUJUKNNVEVJWJULUNUOVFWRPZVQVRWDVHMVFRPZXCABCDUPZVFUIUAVDVEVFUJUK
      UQURNNVGSZNNVKSVOVPUSVGRPZXFXDXGXEVFTUAVGUBUAVIVJWGWJUTEFVGVKVAVBVC $.
      $( [30-Jun-2006] $) $( [10-Mar-2006] $)
  $}

  ${
    $d x y S $.  $d x y T $.
    nmopcoadj.1 $e |- T e. BndLinOp $.
    $( The norm of an operator composed with its adjoint.  Part of Theorem
       3.11(vi) of [Beran] p. 106. $)
    nmopcoadj $p |- ( normop ` ( ( adj ` T ) o. T ) )
                    = ( ( normop ` T ) ^ 2 ) $=
      ( vx cado cfv ccom cnop c2 cexp co wceq cle wbr cbo wcel cr adjbdlnb
      mpbi bdopco nmopret ax-mp resqcl letri3 chil wf cxr cv cno c1 wi wral
      bdopft hocof rexrt wa cmul hoco fveq2d adantr ffvrni syl normclt
      axmulrcl mpan remulcl a1i nmbdoplb w3a cc0 nmopge0t lemul2it mpanr1
      mp3anl3 mpanl2 1re sylan recn mulid1 syl6breq letrd sylanc eqbrtrd
      nmopadj opreq1i sqval eqtr4 ex rgen nmopleubt mp3an csqr sqrcl csp cabs
      cc ax-hicl mpancom absclt bcst hococl mp3anl2 jca normge0t anim1i recnd
      ax1id eqtr4d breqtrd absidt resqclt sqge0t normsqt cdm bdopadjt adj2tOLD
      mp3an1 adjadjt fveq1i opreq2i syl5eqr eqtrd eqtr3d sqsqr 3brtr4d wb
      sqrge0 pm3.2i le2sqt mpan2 mpbird le2sq mp2an breqtr mpbir2an ) ADEZAFZGE
      ZAGEZHIJZKYQYSLMZYSYQLMYQYSYPNOYQPOZYOAANOZYONOZBAQRZBSZYPTUAZYRUUBYRPOZB
      ATUAZUBZUCUDUDYPUEZYSUFOZCUGZUHEZUILMZUULYPEZUHEZYSLMZUJZCUDUKYTYOAUUCUDU
      DYOUEZUUDYOULUAZUUBUDUDAUEZBAULUAZUMZYSPOUUKUUIYSUNUAUURCUDUULUDOZUUNUUQU
      VDUUNUOZUUPYOGEZYRUPJZYSLUVEUUPUULAEZYOEZUHEZUVGLUVDUUPUVJKUUNUVDUUOUVIUH
      UULYOAUUTUVBUQURZUSUVEUVJUVFUVHUHEZUPJZUVGUVDUVJPOZUUNUVDUVIUDOZUVNUVDUVH
      UDOZUVOUDUDUULAUVBUTZUDUDUVHYOUUTUTVAZUVIVBVAZUSUVDUVMPOZUUNUVDUVLPOZUVTU
      VDUVPUWAUVQUVHVBVAZUVFPOZUWAUVTUUCUWCUUDYOTUAZUVFUVLVCVDVAUSUVGPOUVEUVFYR
      UWDUUHVEVFUVDUVJUVMLMZUUNUVDUVPUWEUVQUVHYOUUDVGVAUSUWAUVLYRLMZUVMUVGLMZUV
      EUWAUUGUWFUWGUUHUWAUUGUWCUWFUWGUWDUWAUUGUWCVHVIUVFLMZUWFUWGUUSUWHUUTYOVJU
      AUVLYRUVFVKVLVMVNUVDUWAUUNUWBUSZUVEUVLYRUUMUPJZYRUWIUVDUWJPOZUUNUVDUUMPOZ
      UWKUULVBZUUGUWLUWKUUHYRUUMVCVDVAUSUUGUVEUUHVFUVDUVLUWJLMUUNUULABVGUSUVEUW
      JYRUIUPJZYRLUWLUUNUWJUWNLMZUVDUWLUIPOZUUNUWOVOUWLUWPUUGUUNUWOUUHUWLUWPUUG
      VHVIYRLMZUUNUWOUVAUWQUVBAVJUAZUUMUIYRVKVLVMVNUWMVPYRYRUUHVQZVRVSVTWAVTWBU
      VGYRYRUPJYSUVFYRYRUPABWCWDYRUWSWEWFVSWGWHCYSYPWIWJYSYQWKEZHIJZYQLYRUWTLMZ
      YSUXALMZUVAUWTUFOZUUNUVLUWTLMZUJZCUDUKUXBUVBUWTPOZUXDVIYQLMZUXGUUJUXHUVCY
      PVJUAZYQUUFWLUAZUWTUNUAUXFCUDUVDUUNUXEUVEUXEUVLHIJZUXALMZUVEUVIUULWMJZWNE
      ZYQUXKUXALUVEUXNUVJUUMUPJZYQUVDUXNPOZUUNUVDUXMWOOZUXPUVOUVDUXQUVRUVIUULWP
      WQUXMWRVAUSUVDUXOPOZUUNUVNUWLUXRUVDUVJUUMVCUVSUWMWAUSZUUAUVEUUFVFZUVDUXNU
      XOLMZUUNUVOUVDUYAUVRUVIUULWSWQUSUVEUXOUUPYQUXSUVDUUPPOZUUNUVDUUOUDOUYBUUL
      YOAUUTUVBWTUUOVBVAUSZUXTUVEUXOUVJUIUPJZUUPLUWLUVNUOZVIUVJLMZUUNUOZUXOUYDL
      MZUVEUWLUWPUVNUYGUYHVOUUMUIUVJVKXAUVDUYEUUNUVDUWLUVNUWMUVSXBUSUVDUYFUUNUV
      DUVOUYFUVRUVIXCVAXDWAUVDUYDUUPKUUNUVDUYDUVJUUPUVDUVJWOOUYDUVJKUVDUVJUVSXE
      UVJXFVAUVKXGUSXHUVEUUPYQUUMUPJZYQUYCUVDUYIPOZUUNUVDUWLUYJUWMUUAUWLUYJUUFY
      QUUMVCVDVAUSUXTUVDUUPUYILMUUNUULYPUUEVGUSUVEUYIYQUIUPJZYQLUWLUUNUYIUYKLMZ
      UVDUWLUXHUUNUYLUXIUWLUWPUXHUUNUOZUYLVOUWLUWPUUAUYMUYLUUFUUMUIYQVKVMVNVLUW
      MVPYQYQUUFVQVRVSVTVTVTUVDUXKUXNKUUNUVDUXKWNEZUXKUXNUVDUWAUYNUXKKZUWBUXKPO
      VIUXKLMUYOUWAUXKXIUVLXJUVLXKWAVAUVDUXKUXMWNUVDUXKUVHUVHWMJZUXMUVDUVPUXKUY
      PKUVQUVHXLVAUVDUVHUULYODEZEZWMJZUXMUYPUVPUVDUYSUXMKZUVQYODXMZOZUVPUVDUYTU
      UCVUBUUDYOXNUAUVHUULYOXOXPWQUYRUVHUVHWMUULUYQAAVUAOZUYQAKUUBVUCBAXNUAAXQU
      AXRXSXTYAURYBUSUXAYQKZUVEUXHVUDUXIYQUUFYCUAZVFYDUVDUXEUXLYEZUUNUWAVIUVLLM
      ZVUFUVDUWAVUGUOUXGVIUWTLMZUOVUFUXGVUHUXJUXHVUHUXIYQUUFYFUAZYGUVLUWTYHYIUW
      BUVDUVPVUGUVQUVHXCVAWAUSYJWGWHCUWTAWIWJUWQVUHUXBUXCYEUWRVUIYRUWTUUHUXJYKY
      LRVUEYMYN $.
      $( [13-Jun-2006] $) $( [8-Mar-2006] $)

    $( The norm of an operator composed with its adjoint.  Part of Theorem
       3.11(vi) of [Beran] p. 106. $)
    nmopcoadj2 $p |- ( normop ` ( T o. ( adj ` T ) ) )
                    = ( ( normop ` T ) ^ 2 ) $=
      ( cado cfv ccom cnop c2 cexp co cbo wcel adjbdlnt ax-mp nmopcoadj cdm
      wceq bdopadjt adjadjt coeq1i fveq2i nmopadj opreq1i 3eqtr3 ) ACDZCDZUDEZF
      DUDFDZGHIAUDEZFDAFDZGHIUDAJKZUDJKBALMNUFUHFUEAUDACOKZUEAPUJUKBAQMARMSTUGU
      IGHABUAUBUC $.
      $( [14-Jun-2006] $) $( [10-Mar-2006] $)

    $( An operator composed with its adjoint is zero iff the operator is zero.
       Theorem 3.11(vii) of [Beran] p. 106. $)
    nmopcoadj0 $p |- ( ( T o. ( adj ` T ) ) = ( H~ X. 0H ) <->
                    T = ( H~ X. 0H ) ) $=
      ( cado cfv ccom cnop cc0 wceq chil c0h cxp c2 cexp co nmopcoadj2 eqeq1i
      cbo wcel cr nmopret ax-mp recn sq00 bitr clo bdoplnt adjbdlnt lnopco
      nmlnop0OLD 3bitr3 ) AACDZEZFDZGHZAFDZGHZULIJKZHAUQHUNUOLMNZGHUPUMURGABOPU
      OUOAQRZUOSRBATUAUBUCUDULAUKUSAUERBAUFUAZUKQRZUKUERUSVABAUGUAUKUFUAUHUIAUT
      UIUJ $.
      $( [17-Dec-2006] $) $( [10-Mar-2006] $)
  $}

