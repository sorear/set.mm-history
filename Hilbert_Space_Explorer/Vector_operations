$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Vector operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The Hilbert space scalar product operation is a set. $)
  hvmulex $p |- .h e. _V $=
    ( cc chil cxp csm wf cvv wcel ax-hfvmul cnex ax-hilex xpex fex mp2an ) ABCZ
    BDENFGDFGHABIJKNBFDLM $.
    $( [17-Apr-2007] $)

  $( Closure of vector addition. $)
  hvaddcl $p |- ( ( A e. ~H /\ B e. ~H ) -> ( A +h B ) e. ~H ) $=
    ( chil cva ax-hfvadd foprcl ) ABCCCDEF $.
    $( [18-Apr-2007] $)

  $( Closure of scalar multiplication. $)
  hvmulcl $p |- ( ( A e. CC /\ B e. ~H ) -> ( A .h B ) e. ~H ) $=
    ( chil cc csm ax-hfvmul foprcl ) ABCDCEFG $.
    $( [19-Apr-2007] $)

  ${
    hvmulcl.1 $e |- A e. CC $.
    hvmulcl.2 $e |- B e. ~H $.
    $( Closure inference for scalar multiplication. $)
    hvmulcli $p |- ( A .h B ) e. ~H $=
      ( cc wcel chil csm co hvmulcl mp2an ) AEFBGFABHIGFCDABJK $.
      $( [1-Aug-1999] $)
  $}

  ${
    $d x y $.
    $( Mapping domain and codomain of vector subtraction. $)
    hvsubopr $p |- -h : ( ~H X. ~H ) --> ~H $=
      ( vx vy cv c1 cneg csm co cva chil wcel wral cxp cmv wf cc ax-1cn hvmulcl
      negcli mpan hvaddcl sylan2 rgen2a df-hvsub fmpt2 mpbi ) ACZDEZBCZFGZHGZIJ
      ZBIKAIKIILIMNUKABIUHIJZUFIJUIIJZUKUGOJULUMDPRUGUHQSUFUITUAUBABIIUJIMABUCU
      DUE $.
      $( [6-Sep-2007] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( Value of vector subtraction.  (Revised by Mario Carneiro,
       23-Dec-2013.) $)
    hvsubval $p |- ( ( A e. ~H /\ B e. ~H ) ->
                    ( A -h B ) = ( A +h ( -u 1 .h B ) ) ) $=
      ( vx vy chil cv c1 cneg csm co cva cmv opreq1 wceq opreq2d df-hvsub oprex
      opreq2 ovmpt2 ) CDABEECFZGHZDFZIJZKJAUABIJZKJLAUCKJTAUCKMUBBNUCUDAKUBBUAI
      ROCDPAUDKQS $.
      $( [23-Dec-2013] $) $( [5-Sep-1999] $)
  $}

  $( Closure of vector subtraction. $)
  hvsubcl $p |- ( ( A e. ~H /\ B e. ~H ) -> ( A -h B ) e. ~H ) $=
    ( chil wcel wa cmv co c1 cneg csm hvsubval cc ax-1cn negcli hvmulcl hvaddcl
    cva mpan sylan2 eqeltrd ) ACDZBCDZEABFGAHIZBJGZQGZCABKUBUAUDCDZUECDUCLDUBUF
    HMNUCBORAUDPST $.
    $( [17-Aug-1999] $)

  ${
    hvaddcl.1 $e |- A e. ~H $.
    hvaddcl.2 $e |- B e. ~H $.
    $( Closure of vector addition. $)
    hvaddcli $p |- ( A +h B ) e. ~H $=
      ( chil wcel cva co hvaddcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [1-Aug-1999] $)

    $( Commutation of vector addition. $)
    hvcomi $p |- ( A +h B ) = ( B +h A ) $=
      ( chil wcel cva co wceq ax-hvcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [3-Sep-1999] $)

    $( Value of vector subtraction definition. $)
    hvsubvali $p |- ( A -h B ) = ( A +h ( -u 1 .h B ) ) $=
      ( chil wcel cmv co c1 cneg csm cva wceq hvsubval mp2an ) AEFBEFABGHAIJBKH
      LHMCDABNO $.
      $( [3-Sep-1999] $)

    $( Closure of vector subtraction. $)
    hvsubcli $p |- ( A -h B ) e. ~H $=
      ( chil wcel cmv co hvsubcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [2-Aug-1999] $)
  $}

  $( Addition with the zero vector. $)
  hvaddid2 $p |- ( A e. ~H -> ( 0h +h A ) = A ) $=
    ( chil wcel c0v cva co wceq ax-hv0cl ax-hvcom mpan2 ax-hvaddid eqtr3d ) ABC
    ZADEFZDAEFZAMDBCNOGHADIJAKL $.
    $( [18-Oct-1999] $)

  $( Scalar multiplication with the zero vector. $)
  hvmul0 $p |- ( A e. CC -> ( A .h 0h ) = 0h ) $=
    ( cc wcel c0v cc0 csm cmul mul01 opreq1d chil wceq ax-hv0cl ax-hvmul0 ax-mp
    co syl6eq 0cn ax-hvmulass mp3an23 eqtr3d opreq2i syl6req ) ABCZDAEDFOZFOZAD
    FOUCAEGOZDFOZDUEUCUGUDDUCUFEDFAHIDJCZUDDKLDMNZPUCEBCUHUGUEKQLAEDRSTUDDAFUIU
    AUB $.
    $( [30-May-1999] $)

  $( If a scalar product is zero, one of its factors must be zero. $)
  hvmul0or $p |- ( ( A e. CC /\ B e. ~H ) ->
                  ( ( A .h B ) = 0h <-> ( A = 0 \/ B = 0h ) ) ) $=
    ( cc wcel wa csm co c0v cc0 c1 opreq2 ad2antlr adantlr 3eqtr3d hvmul0 ex wi
    wceq eqeq1d syl5ibrcom chil wo wn wne df-ne cdiv cmul recid2 opreq1d simpll
    reccl simplr ax-hvmulass syl3anc ax-hvmulid syl5bir ax-hvmul0 opreq1 adantl
    syl orrd adantr jaod impbid ) ACDZBUADZEZABFGZHRZAIRZBHRZUBZVGVIVLVGVIEZVJV
    KVJUCAIUDZVMVKAIUEVMVNVKVMVNEJAUFGZVHFGZVOHFGZBHVIVPVQRVGVNVHHVOFKLVGVNVPBR
    VIVGVNEZVOAUGGZBFGZJBFGZVPBVEVNVTWARVFVEVNEZVSJBFAUHUIMVRVOCDZVEVFVTVPRVEVN
    WCVFAUKZMVEVFVNUJVEVFVNULVOABUMUNVFWABRVEVNBUOLNMVGVNVQHRZVIVEVNWEVFWBWCWEW
    DVOOUTMMNPUPVAPVGVJVIVKVFVJVIQVEVFVIVJIBFGZHRBUQVJVHWFHAIBFURSTUSVEVKVIQVFV
    EVIVKAHFGZHRAOVKVHWGHBHAFKSTVBVCVD $.
    $( [19-May-2005] $)

  $( Subtraction of a vector from itself. $)
  hvsubid $p |- ( A e. ~H -> ( A -h A ) = 0h ) $=
    ( chil wcel cmv co cc0 csm c0v c1 cneg caddc cva ax-hvmulid opreq1d cc wceq
    ax-1cn negcli ax-hvdistr2 mp3an12 hvsubval anidms 3eqtr4rd negidi ax-hvmul0
    opreq1i syl6eq eqtrd ) ABCZAADEZFAGEZHUIUJIIJZKEZAGEZUKUIIAGEZULAGEZLEZAUPL
    EZUNUJUIUOAUPLAMNIOCULOCUIUNUQPQIQRIULASTUIUJURPAAUAUBUCUMFAGIQUDUFUGAUEUH
    $.
    $( [30-May-1999] $)

  $( Addition of negative of a vector to itself. $)
  hvnegid $p |- ( A e. ~H -> ( A +h ( -u 1 .h A ) ) = 0h ) $=
    ( chil wcel cmv co c1 cneg csm cva c0v wceq hvsubval anidms hvsubid eqtr3d
    ) ABCZAADEZAFGAHEIEZJPQRKAALMANO $.
    $( [4-Jun-2004] $)

  $( Two ways to express the negative of a vector. $)
  hv2neg $p |- ( A e. ~H -> ( 0h -h A ) = ( -u 1 .h A ) ) $=
    ( chil wcel c0v cmv co c1 cneg csm cva wceq ax-hv0cl hvsubval ax-1cn negcli
    mpan cc hvmulcl hvaddid2 syl eqtrd ) ABCZDAEFZDGHZAIFZJFZUEDBCUBUCUFKLDAMPU
    BUEBCZUFUEKUDQCUBUGGNOUDARPUESTUA $.
    $( [23-May-2005] $)

  ${
    hvaddid2.1 $e |- A e. ~H $.
    $( Addition with the zero vector. $)
    hvaddid2i $p |- ( 0h +h A ) = A $=
      ( chil wcel c0v cva co wceq hvaddid2 ax-mp ) ACDEAFGAHBAIJ $.
      $( [18-Aug-1999] $)

    $( Addition of negative of a vector to itself. $)
    hvnegidi $p |- ( A +h ( -u 1 .h A ) ) = 0h $=
      ( chil wcel c1 cneg csm co cva c0v wceq hvnegid ax-mp ) ACDAEFAGHIHJKBALM
      $.
      $( [18-Aug-1999] $)

    $( Two ways to express the negative of a vector. $)
    hv2negi $p |- ( 0h -h A ) = ( -u 1 .h A ) $=
      ( chil wcel c0v cmv co c1 cneg csm wceq hv2neg ax-mp ) ACDEAFGHIAJGKBALM
      $.
      $( [31-Oct-1999] $)
  $}

  $( Convert minus one times a scalar product to the negative of the scalar. $)
  hvm1neg $p |- ( ( A e. CC /\ B e. ~H ) ->
                 ( -u 1 .h ( A .h B ) ) = ( -u A .h B ) ) $=
    ( cc wcel chil wa c1 cneg cmul co csm wceq ax-1cn negcli ax-hvmulass mp3an1
    mulm1 adantr opreq1d eqtr3d ) ACDZBEDZFZGHZAIJZBKJZUDABKJKJZAHZBKJUDCDUAUBU
    FUGLGMNUDABOPUCUEUHBKUAUEUHLUBAQRST $.
    $( [4-Jun-2004] $)

  $( Value of vector addition in terms of vector subtraction. $)
  hvaddsubval $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( A +h B ) = ( A -h ( -u 1 .h B ) ) ) $=
    ( chil wcel wa c1 cneg csm cmv cva wceq ax-1cn negcli hvmulcl mpan hvsubval
    co cc sylan2 hvm1neg negnegi opreq1i syl6eq ax-hvmulid eqtrd adantl opreq2d
    eqtr2d ) ACDZBCDZEZAFGZBHQZIQZAULUMHQZJQZABJQUJUIUMCDZUNUPKULRDZUJUQFLMZULB
    NOAUMPSUKUOBAJUJUOBKUIUJUOFBHQZBUJUOULGZBHQZUTURUJUOVBKUSULBTOVAFBHFLUAUBUC
    BUDUEUFUGUH $.
    $( [10-Jun-2006] $)

  $( Commutative/associative law. $)
  hvadd32 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
               ( ( A +h B ) +h C ) = ( ( A +h C ) +h B ) ) $=
    ( chil wcel w3a co wceq wa ax-hvcom opreq2d 3adant1 ax-hvass 3com23 3eqtr4d
    cva ) ADEZBDEZCDEZFABCPGZPGZACBPGZPGZABPGCPGACPGBPGZRSUAUCHQRSITUBAPBCJKLAB
    CMQSRUDUCHACBMNO $.
    $( [16-Oct-1999] $)

  $( Commutative/associative law. $)
  hvadd12 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
               ( A +h ( B +h C ) ) = ( B +h ( A +h C ) ) ) $=
    ( chil wcel w3a co wceq wa ax-hvcom opreq1d 3adant3 ax-hvass 3com12 3eqtr3d
    cva ) ADEZBDEZCDEZFABPGZCPGZBAPGZCPGZABCPGPGBACPGPGZQRUAUCHSQRITUBCPABJKLAB
    CMRQSUCUDHBACMNO $.
    $( [19-Oct-1999] $)

  $( Hilbert vector space addition law. $)
  hvadd4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
               ( ( A +h B ) +h ( C +h D ) ) = ( ( A +h C ) +h ( B +h D ) ) ) $=
    ( chil wcel wa cva co wceq w3a hvadd32 opreq1d 3expa adantrr ax-hvass 3expb
    hvaddcl sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZBHIZDHI
    ZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUDUHEFZUGUJUNJZA
    BRUQUEUFURUHCDPQSUBUEUCUFUMUOJZUBUEGUKEFZUCUFGUSACRUTUCUFUSUKBDPQSTUA $.
    $( [16-Oct-1999] $)

  $( Hilbert vector space addition/subtraction law. $)
  hvsub4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
               ( ( A +h B ) -h ( C +h D ) ) = ( ( A -h C ) +h ( B -h D ) ) ) $=
    ( chil wcel wa cva co c1 cneg csm cmv wceq cc hvmulcl mpan hvaddcl hvsubval
    anim2i ax-1cn negcli ax-hvdistr1 mp3an1 adantl opreq2d hvadd4 eqtr4d syl2an
    anim12i an4s syl ad2ant2r ad2ant2l opreq12d 3eqtr4d ) AEFZBEFZGZCEFZDEFZGZG
    ZABHIZJKZCDHIZLIZHIZAVECLIZHIZBVEDLIZHIZHIZVDVFMIZACMIZBDMIZHIVCVHVDVIVKHIZ
    HIZVMVCVGVQVDHVBVGVQNZUSVEOFZUTVAVSJUAUBZVECDUCUDUEUFVCUQVIEFZGZURVKEFZGZGZ
    VMVRNUQUTURVAWFUQUTGWCURVAGWEUTWBUQVTUTWBWAVECPQTVAWDURVTVAWDWAVEDPQTUJUKAV
    IBVKUGULUHUSVDEFVFEFVNVHNVBABRCDRVDVFSUIVCVOVJVPVLHUQUTVOVJNURVAACSUMURVAVP
    VLNUQUTBDSUNUOUP $.
    $( [17-Oct-1999] $)

  $( Commutative/associative law. $)
  hvaddsub12 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
               ( A +h ( B -h C ) ) = ( B +h ( A -h C ) ) ) $=
    ( chil wcel w3a c1 cneg csm co cva cmv cc ax-1cn negcli hvmulcl wa hvsubval
    wceq opreq2d mpan hvadd12 syl3an3 3adant1 3adant2 3eqtr4d ) ADEZBDEZCDEZFAB
    GHZCIJZKJZKJZBAUKKJZKJZABCLJZKJZBACLJZKJZUIUGUHUKDEZUMUOSUJMEUIUTGNOUJCPUAA
    BUKUBUCUHUIUQUMSUGUHUIQUPULAKBCRTUDUGUIUSUOSUHUGUIQURUNBKACRTUEUF $.
    $( [19-Oct-1999] $)

  $( Addition/subtraction cancellation law for vectors in Hilbert space. $)
  hvpncan $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( A +h B ) -h B ) = A ) $=
    ( chil wcel wa cva co cmv c1 cneg csm c0v wceq hvaddcl hvsubval sylancom cc
    ax-1cn negcli hvmulcl mpan ancli ax-hvass 3expb sylan2 eqtrd hvnegid adantl
    opreq2d ax-hvaddid adantr 3eqtrd ) ACDZBCDZEZABFGZBHGZABIJZBKGZFGZFGZALFGZA
    UOUQUPUSFGZVAUMUNUPCDUQVCMABNUPBOPUNUMUNUSCDZEVCVAMZUNVDURQDUNVDIRSURBTUAUB
    UMUNVDVEABUSUCUDUEUFUNVAVBMUMUNUTLAFBUGUIUHUMVBAMUNAUJUKUL $.
    $( [7-Jun-2004] $)

  $( Addition/subtraction cancellation law for vectors in Hilbert space. $)
  hvpncan2 $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( A +h B ) -h A ) = B ) $=
    ( chil wcel cva co cmv wceq wa ax-hvcom opreq1d hvpncan eqtr3d ancoms ) BCD
    ZACDZABEFZAGFZBHOPIZBAEFZAGFRBSTQAGBAJKBALMN $.
    $( [7-Jun-2004] $)

  $( Associativity of sum and difference of Hilbert space vectors. $)
  hvaddsubass $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) -> ( ( A +h B ) -h C )
                     = ( A +h ( B -h C ) ) ) $=
    ( chil wcel w3a cva co c1 cneg csm cmv wceq cc ax-1cn hvmulcl mpan ax-hvass
    negcli hvsubval syl3an3 wa hvaddcl sylan 3impa 3adant1 opreq2d 3eqtr4d ) AD
    EZBDEZCDEZFZABGHZIJZCKHZGHZABUOGHZGHZUMCLHZABCLHZGHUKUIUJUODEZUPURMUNNEUKVA
    IOSUNCPQABUORUAUIUJUKUSUPMZUIUJUBUMDEUKVBABUCUMCTUDUEULUTUQAGUJUKUTUQMUIBCT
    UFUGUH $.
    $( [27-Aug-2004] $)

  $( Subtraction and addition of equal Hilbert space vectors.. $)
  hvpncan3 $p |- ( ( A e. ~H /\ B e. ~H ) -> ( A +h ( B -h A ) ) = B ) $=
    ( chil wcel wa cva co cmv wceq hvaddsubass 3anidm13 hvpncan2 eqtr3d ) ACDZB
    CDZEABFGAHGZABAHGFGZBNOPQIABAJKABLM $.
    $( [27-Aug-2004] $)

  $( Scalar multiplication commutative law. $)
  hvmulcom $p |- ( ( A e. CC /\ B e. CC /\ C e. ~H ) ->
                 ( A .h ( B .h C ) ) = ( B .h ( A .h C ) ) ) $=
    ( cc wcel chil w3a cmul co csm wa mulcom opreq1d 3adant3 ax-hvmulass 3com12
    wceq 3eqtr3d ) ADEZBDEZCFEZGABHIZCJIZBAHIZCJIZABCJIJIBACJIJIZSTUCUEQUASTKUB
    UDCJABLMNABCOTSUAUEUFQBACOPR $.
    $( [19-May-2005] $)

  ${
    hvmulcom.1 $e |- A e. CC $.
    hvmulcom.2 $e |- B e. CC $.
    hvmulcom.3 $e |- C e. ~H $.
    $( Scalar multiplication associative law. $)
    hvmulassi $p |- ( ( A x. B ) .h C ) = ( A .h ( B .h C ) ) $=
      ( cc wcel chil cmul co csm wceq ax-hvmulass mp3an ) AGHBGHCIHABJKCLKABCLK
      LKMDEFABCNO $.
      $( [3-Sep-1999] $)

    $( Scalar multiplication commutative law. $)
    hvmulcomi $p |- ( A .h ( B .h C ) ) = ( B .h ( A .h C ) ) $=
      ( cc wcel chil csm co wceq hvmulcom mp3an ) AGHBGHCIHABCJKJKBACJKJKLDEFAB
      CMN $.
      $( [3-Sep-1999] $)

    $( Double negative in scalar multiplication. $)
    hvmul2negi $p |- ( -u A .h ( -u B .h C ) ) = ( A .h ( B .h C ) ) $=
      ( cneg cmul co csm mul2negi opreq1i negcli hvmulassi 3eqtr3i ) AGZBGZHIZC
      JIABHIZCJIPQCJIJIABCJIJIRSCJABDEKLPQCADMBEMFNABCDEFNO $.
      $( [3-Sep-1999] $)
  $}

  $( Scalar multiplication distributive law for subtraction. $)
  hvsubdistr1 $p |- ( ( A e. CC /\ B e. ~H /\ C e. ~H ) ->
                    ( A .h ( B -h C ) ) = ( ( A .h B ) -h ( A .h C ) ) ) $=
    ( cc wcel chil w3a c1 cneg csm co cva ax-1cn negcli hvmulcl opreq2d 3adant2
    cmv wceq hvsubval mpan ax-hvdistr1 syl3an3 wa hvmulcom mp3an2 eqtrd 3adant1
    3adant3 syl2anc 3eqtr4d ) ADEZBFEZCFEZGZABHIZCJKZLKZJKZABJKZUPACJKZJKZLKZAB
    CRKZJKUTVARKZUOUSUTAUQJKZLKZVCUNULUMUQFEZUSVGSUPDEZUNVHHMNZUPCOUAABUQUBUCUL
    UNVGVCSUMULUNUDVFVBUTLULVIUNVFVBSVJAUPCUEUFPQUGUOVDURAJUMUNVDURSULBCTUHPUOU
    TFEZVAFEZVEVCSULUMVKUNABOUIULUNVLUMACOQUTVATUJUK $.
    $( [19-May-2005] $)

  $( Scalar multiplication distributive law for subtraction. $)
  hvsubdistr2 $p |- ( ( A e. CC /\ B e. CC /\ C e. ~H ) -> ( ( A - B ) .h C
      ) = ( ( A .h C ) -h ( B .h C ) ) ) $=
    ( cc wcel chil w3a cmin co csm c1 cneg cva caddc wceq negcl opreq1d 3adant1
    cmv hvmulcl ax-hvdistr2 syl3an2 negsub 3adant3 wa cmul adantr ax-1cn negcli
    ax-hvmulass mp3an1 eqtr3d opreq2d 3eqtr3d 3adant2 hvsubval syl2anc eqtr4d
    mulm1 ) ADEZBDEZCFEZGZABHIZCJIZACJIZKLZBCJIZJIZMIZVFVHSIZVCABLZNIZCJIZVFVLC
    JIZMIZVEVJVAUTVLDEVBVNVPOBPAVLCUAUBVCVMVDCJUTVAVMVDOVBABUCUDQVCVOVIVFMVAVBV
    OVIOUTVAVBUEVGBUFIZCJIZVOVIVAVRVOOVBVAVQVLCJBUSQUGVGDEVAVBVRVIOKUHUIVGBCUJU
    KULRUMUNVCVFFEZVHFEZVKVJOUTVBVSVAACTUOVAVBVTUTBCTRVFVHUPUQUR $.
    $( [1-Aug-2006] $) $( [19-May-2005] $)

  ${
    hvdistr1.1 $e |- A e. CC $.
    hvdistr1.2 $e |- B e. ~H $.
    hvdistr1.3 $e |- C e. ~H $.
    $( Scalar multiplication distributive law. $)
    hvdistr1i $p |- ( A .h ( B +h C ) ) = ( ( A .h B ) +h ( A .h C ) ) $=
      ( cc wcel chil cva co csm wceq ax-hvdistr1 mp3an ) AGHBIHCIHABCJKLKABLKAC
      LKJKMDEFABCNO $.
      $( [3-Sep-1999] $)

    $( Scalar multiplication distributive law. $)
    hvsubdistr1i $p |- ( A .h ( B -h C ) ) = ( ( A .h B ) -h ( A .h C ) ) $=
      ( cc wcel chil cmv co csm wceq hvsubdistr1 mp3an ) AGHBIHCIHABCJKLKABLKAC
      LKJKMDEFABCNO $.
      $( [3-Sep-1999] $)
  $}

  ${
    hvass.1 $e |- A e. ~H $.
    hvass.2 $e |- B e. ~H $.
    hvass.3 $e |- C e. ~H $.
    $( Hilbert vector space associative law. $)
    hvassi $p |- ( ( A +h B ) +h C ) = ( A +h ( B +h C ) ) $=
      ( chil wcel cva co wceq ax-hvass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABCLM
      $.
      $( [3-Sep-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvadd32i $p |- ( ( A +h B ) +h C ) = ( ( A +h C ) +h B ) $=
      ( chil wcel cva co wceq hvadd32 mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM
      $.
      $( [18-Aug-1999] $)

    $( Hilbert vector space associative law for subtraction. $)
    hvsubassi $p |- ( ( A -h B ) -h C ) = ( A -h ( B +h C ) ) $=
      ( cmv co c1 cneg csm cva hvsubcli hvsubvali ax-1cn negcli hvmulcli hvassi
      hvdistr1i opreq2i eqtr4i opreq1i hvaddcli 3eqtr4i eqtri ) ABGHZCGHUFIJZCK
      HZLHZABCLHZGHZUFCABDEMFNAUGBKHZLHZUHLHZAUGUJKHZLHZUIUKUNAULUHLHZLHUPAULUH
      DUGBIOPZEQUGCURFQRUOUQALUGBCUREFSTUAUFUMUHLABDENUBAUJDBCEFUCNUDUE $.
      $( [7-Oct-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvsub32i $p |- ( ( A -h B ) -h C ) = ( ( A -h C ) -h B ) $=
      ( cva co cmv hvcomi opreq2i hvsubassi 3eqtr4i ) ABCGHZIHACBGHZIHABIHCIHAC
      IHBIHNOAIBCEFJKABCDEFLACBDFELM $.
      $( [7-Oct-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvadd12i $p |- ( A +h ( B +h C ) ) = ( B +h ( A +h C ) ) $=
      ( cva co hvcomi opreq1i hvassi 3eqtr3i ) ABGHZCGHBAGHZCGHABCGHGHBACGHGHMN
      CGABDEIJABCDEFKBACEDFKL $.
      $( [11-Sep-1999] $)

    ${
      hvadd4.4 $e |- D e. ~H $.
      $( Hilbert vector space addition law. $)
      hvadd4i $p |- ( ( A +h B ) +h ( C +h D ) ) =
                   ( ( A +h C ) +h ( B +h D ) ) $=
        ( chil wcel cva co wceq hvadd4 mp4an ) AIJBIJCIJDIJABKLCDKLKLACKLBDKLKL
        MEFGHABCDNO $.
        $( [3-Sep-1999] $)

      $( Hilbert vector space addition law. $)
      hvsubsub4i $p |- ( ( A -h B ) -h ( C -h D ) ) =
                      ( ( A -h C ) -h ( B -h D ) ) $=
        ( c1 csm cva cmv hvmulcli hvdistr1i opreq2i hvaddcli hvsubvali opreq12i
        co 3eqtr4i cneg ax-1cn negcli hvadd4i ) AIUAZBJSZKSZCUEDJSZKSZLSZAUECJS
        ZKSZBUHKSZLSZABLSZCDLSZLSACLSZBDLSZLSUGUEUIJSZKSZULUEUMJSZKSZUJUNUGUKUE
        UHJSZKSZKSULUFVCKSZKSUTVBAUFUKVCEUEBIUBUCZFMZUECVFGMZUEUHVFUEDVFHMZMUDU
        SVDUGKUECUHVFGVINOVAVEULKUEBUHVFFVINOTUGUIAUFEVGPCUHGVIPQULUMAUKEVHPBUH
        FVIPQTUOUGUPUILABEFQCDGHQRUQULURUMLACEGQBDFHQRT $.
        $( [31-Oct-1999] $)
    $}
  $}

  $( Hilbert vector space addition/subtraction law. $)
  hvsubsub4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) )
            -> ( ( A -h B ) -h ( C -h D ) ) = ( ( A -h C ) -h ( B -h D ) ) ) $=
    ( chil wcel cmv wceq c0v cif opreq1 opreq1d eqeq12d opreq2 opreq2d ax-hv0cl
    co elimel hvsubsub4i dedth4h ) AEFZBEFZCEFZDEFZABGQZCDGQZGQZACGQZBDGQZGQZHU
    AAIJZBGQZUFGQZUKCGQZUIGQZHUKUBBIJZGQZUFGQZUNUPDGQZGQZHUQUCCIJZDGQZGQZUKVAGQ
    ZUSGQZHUQVAUDDIJZGQZGQZVDUPVFGQZGQZHABCDIIIIAUKHZUGUMUJUOVKUEULUFGAUKBGKLVK
    UHUNUIGAUKCGKLMBUPHZUMURUOUTVLULUQUFGBUPUKGNLVLUIUSUNGBUPDGKOMCVAHZURVCUTVE
    VMUFVBUQGCVADGKOVMUNVDUSGCVAUKGNLMDVFHZVCVHVEVJVNVBVGUQGDVFVAGNOVNUSVIVDGDV
    FUPGNOMUKUPVAVFAIEPRBIEPRCIEPRDIEPRST $.
    $( [2-Apr-2000] $)

  $( Two times a vector. $)
  hv2times $p |- ( A e. ~H -> ( 2 .h A ) = ( A +h A ) ) $=
    ( chil wcel c2 csm co c1 cva caddc df-2 opreq1i cc wceq ax-hvdistr2 mp3an12
    ax-1cn syl5eq ax-hvdistr1 mp3an1 anidms hvaddcl ax-hvmulid syl 3eqtr2d ) AB
    CZDAEFZGAEFZUGHFZGAAHFZEFZUIUEUFGGIFZAEFZUHDUKAEJKGLCZUMUEULUHMPPGGANOQUEUJ
    UHMZUMUEUEUNPGAARSTUEUIBCZUJUIMUEUOAAUATUIUBUCUD $.
    $( [22-Jun-2006] $)

  ${
    hvnegdi.1 $e |- A e. ~H $.
    hvnegdi.2 $e |- B e. ~H $.
    $( Distribution of negative over subtraction. $)
    hvnegdii $p |- ( -u 1 .h ( A -h B ) ) = ( B -h A ) $=
      ( c1 cneg cmv co csm cva hvsubvali opreq2i ax-1cn hvmulcli hvdistr1i cmul
      negcli mul2negi mulid1i opreq1i eqtri hvmulassi chil wcel wceq ax-hvmulid
      ax-mp 3eqtr3i hvcomi 3eqtr4i 3eqtri ) EFZABGHZIHULAULBIHZJHZIHULAIHZULUNI
      HZJHZBAGHZUMUOULIABCDKLULAUNEMQZCULBUTDNZOUQUPJHBUPJHURUSUQBUPJULULPHZBIH
      EBIHZUQBVBEBIVBEEPHEEEMMREMSUATULULBUTUTDUBBUCUDVCBUEDBUFUGUHTUPUQULAUTCN
      ULUNUTVANUIBADCKUJUK $.
      $( [31-Oct-1999] $)

    $( If the difference between two vectors is zero, they are equal. $)
    hvsubeq0i $p |- ( ( A -h B ) = 0h <-> A = B ) $=
      ( cmv co c0v wceq c1 cneg csm cva hvsubvali eqeq1i opreq1 sylbi chil wcel
      ax-1cn ax-mp negcli hvmulcli hvadd32i hvassi opreq2i ax-hvaddid hvaddid2i
      hvnegidi eqtri 3eqtri 3eqtr3g hvsubid syl6eq impbii ) ABEFZGHZABHZUPAIJZB
      KFZLFZBLFZGBLFZABUPUTGHVAVBHUOUTGABCDMNUTGBLOPVAABLFUSLFABUSLFZLFZAAUSBCU
      RBISUADUBZDUCABUSCDVEUDVDAGLFZAVCGALBDUHUEAQRVFAHCAUFTUIUJBDUGUKUQUOBBEFZ
      GABBEOBQRVGGHDBULTUMUN $.
      $( [18-Aug-1999] $)

    $( Vector cancellation law. $)
    hvsubcan2i $p |- ( ( A +h B ) +h ( A -h B ) ) = ( 2 .h A ) $=
      ( cva co cmv c1 cneg csm c2 c0v hvsubvali opreq2i hvmulcli chil wcel wceq
      ax-1cn ax-mp negcli hvadd4i hv2times eqcomi hvnegidi eqtri 2cn ax-hvaddid
      opreq12i 3eqtri ) ABEFZABGFZEFUKAHIZBJFZEFZEFZKAJFZLEFZUQULUOUKEABCDMNUPA
      AEFZBUNEFZEFURABAUNCDCUMBHSUADOUBUSUQUTLEUQUSAPQUQUSRCAUCTUDBDUEUIUFUQPQU
      RUQRKAUGCOUQUHTUJ $.
      $( [3-Sep-1999] $)

    ${
      hvaddcan.3 $e |- C e. ~H $.
      $( Cancellation law for vector addition. $)
      hvaddcani $p |- ( ( A +h B ) = ( A +h C ) <-> B = C ) $=
        ( cva wceq cneg csm opreq1 c0v ax-1cn negcli hvadd32i opreq1i hvaddid2i
        co c1 3eqtri hvmulcli hvnegidi 3eqtr3g opreq2 impbii ) ABGRZACGRZHZBCHU
        HUFSIZAJRZGRZUGUJGRZBCUFUGUJGKUKAUJGRZBGRLBGRBABUJDEUIASMNDUAZOUMLBGADU
        BZPBEQTULUMCGRLCGRCACUJDFUNOUMLCGUOPCFQTUCBCAGUDUE $.
        $( [11-Sep-1999] $)

      $( Relationship between vector subtraction and addition. $)
      hvsubaddi $p |- ( ( A -h B ) = C <-> ( B +h C ) = A ) $=
        ( cmv co wceq c1 cneg csm cva hvsubvali eqeq1i ax-1cn hvmulcli hvadd12i
        c0v negcli hvnegidi opreq2i chil wcel ax-hvaddid ax-mp 3eqtri hvaddcani
        hvaddcli eqcom 3bitr3i bitri ) ABGHZCIAJKZBLHZMHZCIZBCMHZAIZUMUPCABDENO
        BUPMHZURIAURIUQUSUTAURUTABUOMHZMHASMHZABAUOEDUNBJPTEQZRVASAMBEUAUBAUCUD
        VBAIDAUEUFUGOBUPCEAUODVCUIFUHAURUJUKUL $.
        $( [11-Sep-1999] $)
    $}
  $}

  $( Distribution of negative over subtraction. $)
  hvnegdi $p |- ( ( A e. ~H /\ B e. ~H ) ->
                ( -u 1 .h ( A -h B ) ) = ( B -h A ) ) $=
    ( chil wcel c1 cneg cmv co csm wceq c0v cif opreq1 opreq2d eqeq12d ax-hv0cl
    opreq2 elimel hvnegdii dedth2h ) ACDZBCDZEFZABGHZIHZBAGHZJUCUAAKLZBGHZIHZBU
    GGHZJUCUGUBBKLZGHZIHZUKUGGHZJABKKAUGJZUEUIUFUJUOUDUHUCIAUGBGMNAUGBGQOBUKJZU
    IUMUJUNUPUHULUCIBUKUGGQNBUKUGGMOUGUKAKCPRBKCPRST $.
    $( [2-Apr-2000] $)

  $( If the difference between two vectors is zero, they are equal. $)
  hvsubeq0 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( A -h B ) = 0h <-> A = B ) ) $=
    ( chil wcel cmv co c0v wceq wb cif opreq1 eqeq1d eqeq1 bibi12d opreq2 eqeq2
    ax-hv0cl elimel hvsubeq0i dedth2h ) ACDZBCDZABEFZGHZABHZIUAAGJZBEFZGHZUFBHZ
    IUFUBBGJZEFZGHZUFUJHZIABGGAUFHZUDUHUEUIUNUCUGGAUFBEKLAUFBMNBUJHZUHULUIUMUOU
    GUKGBUJUFEOLBUJUFPNUFUJAGCQRBGCQRST $.
    $( [23-Oct-1999] $)

  $( If the sum of two vectors is zero, one is the negative of the other. $)
  hvaddeq0 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( A +h B ) = 0h <-> A = ( -u 1 .h B ) ) ) $=
    ( chil wcel wa cva co c0v wceq c1 cneg csm cmv hvaddsubval eqeq1d wb ax-1cn
    cc negcli hvmulcl mpan hvsubeq0 sylan2 bitrd ) ACDZBCDZEZABFGZHIAJKZBLGZMGZ
    HIZAUJIZUGUHUKHABNOUFUEUJCDZULUMPUIRDUFUNJQSUIBTUAAUJUBUCUD $.
    $( [10-Jun-2006] $)

  $( Cancellation law for vector addition. $)
  hvaddcan $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A +h B ) = ( A +h C ) <-> B = C ) ) $=
    ( chil wcel cva co wceq c0v cif opreq1 eqeq12d bibi1d opreq2 eqeq1d bibi12d
    wb eqeq1 ax-hv0cl elimel eqeq2d eqeq2 hvaddcani dedth3h ) ADEZBDEZCDEZABFGZ
    ACFGZHZBCHZQUEAIJZBFGZULCFGZHZUKQULUFBIJZFGZUNHZUPCHZQUQULUGCIJZFGZHZUPUTHZ
    QABCIIIAULHZUJUOUKVDUHUMUIUNAULBFKAULCFKLMBUPHZUOURUKUSVEUMUQUNBUPULFNOBUPC
    RPCUTHZURVBUSVCVFUNVAUQCUTULFNUACUTUPUBPULUPUTAIDSTBIDSTCIDSTUCUD $.
    $( [18-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvaddcan2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A +h C ) = ( B +h C ) <-> A = B ) ) $=
    ( chil wcel cva co wceq wb ax-hvcom 3adant3 3adant2 eqeq12d hvaddcan bitr3d
    w3a 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIRSTPZCAFGZCBFGZHUCUDUEUFUAUGUBRSUF
    UAHTCAJKRTUGUBHSCBJLMCABNOQ $.
    $( [18-May-2005] $)

  $( Cancellation law for scalar multiplication. $)
  hvmulcan $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ B e. ~H /\ C e. ~H )
               -> ( ( A .h B ) = ( A .h C ) <-> B = C ) ) $=
    ( cc wcel cc0 wne wa chil w3a cmv co c0v wo csm wb 3adant3 hvsubeq0 hvmulcl
    wceq df-ne biorf sylbi ad2antlr 3adant1 hvsubdistr1 eqeq1d hvsubcl hvmul0or
    wn sylan2 3impb 3adant2 syl2anc 3bitr3d 3adant1r 3bitr3rd ) ADEZAFGZHZBIEZC
    IEZJBCKLZMTZAFTZVDNZBCTZABOLZACOLZTZUTVAVDVFPZVBUSVKURVAUSVEUJVKAFUAVEVDUBU
    CUDQVAVBVDVGPUTBCRUEURVAVBVFVJPUSURVAVBJZAVCOLZMTZVHVIKLZMTZVFVJVLVMVOMABCU
    FUGURVAVBVNVFPZVAVBHURVCIEVQBCUHAVCUIUKULVLVHIEZVIIEZVPVJPURVAVRVBABSQURVBV
    SVAACSUMVHVIRUNUOUPUQ $.
    $( [30-Dec-2008] $) $( [19-May-2005] $)

  $( Cancellation law for scalar multiplication. $)
  hvmulcan2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. ~H /\ C =/= 0h ) ) ->
                  ( ( A .h C ) = ( B .h C ) <-> A = B ) ) $=
    ( cc wcel chil c0v wne wa w3a csm co cmv wb hvmulcl 3adant1 3adant3r bitr3d
    wceq wo 3adant2 hvsubeq0 syl2anc cmin cc0 hvsubdistr2 eqeq1d subcl hvmul0or
    sylan 3impa df-ne biorf orcom syl6bb sylbi ad2antll subeq0 3adant3 3bitr2d
    wn ) ADEZBDEZCFEZCGHZIZJZACKLZBCKLZMLZGSZVHVISZABSZVBVCVDVKVLNZVEVBVCVDJZVH
    FEZVIFEZVNVBVDVPVCACOUAVCVDVQVBBCOPVHVIUBUCQVGVKABUDLZUESZCGSZTZVSVMVBVCVDV
    KWANVEVOVRCKLZGSZVKWAVOWBVJGABCUFUGVBVCVDWCWANZVBVCIVRDEVDWDABUHVRCUIUJUKRQ
    VCVFVSWANZVBVEWEVCVDVEVTVAZWECGULWFVSVTVSTWAVTVSUMVTVSUNUOUPUQPVBVCVSVMNVFA
    BURUSUTR $.
    $( [2-Jan-2009] $) $( [19-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvsubcan $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h B ) = ( A -h C ) <-> B = C ) ) $=
    ( chil wcel w3a cmv co wceq c1 cneg csm cva hvsubval 3adant3 3adant2 ax-1cn
    wb hvmulcl mpan eqeq12d negcli hvaddcan syl3an3 syl3an2 cc0 ax-1ne0 negne0i
    cc wne wa pm3.2i hvmulcan mp3an1 3adant1 3bitrd ) ADEZBDEZCDEZFZABGHZACGHZI
    AJKZBLHZMHZAVCCLHZMHZIZVDVFIZBCIZUTVAVEVBVGUQURVAVEIUSABNOUQUSVBVGIURACNPUA
    URUQVDDEZUSVHVIRZVCUIEZURVKJQUBZVCBSTUSUQVKVFDEZVLVMUSVOVNVCCSTAVDVFUCUDUEU
    RUSVIVJRZUQVMVCUFUJZUKURUSVPVMVQVNJQUGUHULVCBCUMUNUOUP $.
    $( [18-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvsubcan2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h C ) = ( B -h C ) <-> A = B ) ) $=
    ( chil wcel cmv co wceq wb w3a c1 csm hvsubcl 3adant3 3adant2 cc cc0 ax-1cn
    cneg hvnegdi wne wa negcli ax-1ne0 negne0i pm3.2i hvmulcan syl2anc hvsubcan
    mp3an1 eqeq12d 3bitr3d 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIUNUOUPJZKSZCAFG
    ZLGZVBCBFGZLGZHZVCVEHZUSUTVAVCDEZVEDEZVGVHIZUNUOVIUPCAMNUNUPVJUOCBMOVBPEZVB
    QUAZUBVIVJVKVLVMKRUCKRUDUEUFVBVCVEUGUJUHVAVDUQVFURUNUOVDUQHUPCATNUNUPVFURHU
    OCBTOUKCABUIULUM $.
    $( [18-May-2005] $)

  $( Subtraction of a zero vector. $)
  hvsub0 $p |- ( A e. ~H -> ( A -h 0h ) = A ) $=
    ( chil wcel c0v cmv co cva c1 cneg csm wceq ax-hv0cl hvsubval ax-1cn negcli
    mpan2 cc hvmul0 ax-mp opreq2i syl6eq ax-hvaddid eqtrd ) ABCZADEFZADGFZAUDUE
    AHIZDJFZGFZUFUDDBCUEUIKLADMPUHDAGUGQCUHDKHNOUGRSTUAAUBUC $.
    $( [2-Apr-2000] $)

  $( Relationship between vector subtraction and addition. $)
  hvsubadd $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h B ) = C <-> ( B +h C ) = A ) ) $=
    ( chil wcel cmv co wceq cva wb c0v cif opreq1 eqeq1d eqeq2 bibi12d ax-hv0cl
    opreq2 elimel hvsubaddi dedth3h ) ADEZBDEZCDEZABFGZCHZBCIGZAHZJUBAKLZBFGZCH
    ZUGUIHZJUIUCBKLZFGZCHZUMCIGZUIHZJUNUDCKLZHZUMURIGZUIHZJABCKKKAUIHZUFUKUHULV
    BUEUJCAUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFRNVCUGUPUIBUMCIMNPCURHZUOUSUQ
    VACURUNOVDUPUTUICURUMIRNPUIUMURAKDQSBKDQSCKDQSTUA $.
    $( [30-Oct-1999] $)

  $( Hilbert vector space addition/subtraction law. $)
  hvaddsub4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
          ( ( A +h B ) = ( C +h D ) <-> ( A -h C ) = ( D -h B ) ) ) $=
    ( chil wcel wa cva co cmv wceq hvaddcl ancoms hvsub4 syldan hvsubid hvsubcl
    c0v syl 3eqtrd wb adantr adantl ad2ant2lr hvsubcan2 syl3anc anim2i ad2antlr
    opreq2d ax-hvaddid adantlr adantrr anim1i ad2antrr opreq1d hvaddid2 adantll
    simpr simpl eqeq12d bitr3d ) AEFZBEFZGZCEFZDEFZGZGZABHIZCBHIZJIZCDHIZVJJIZK
    ZVIVLKZACJIZDBJIZKVHVIEFZVLEFZVJEFZVNVOUAVDVRVGABLUBVGVSVDCDLUCVCVEVTVBVFVE
    VCVTCBLMUDVIVLVJUEUFVHVKVPVMVQVDVEVKVPKVFVDVEGZVKVPBBJIZHIZVPRHIZVPVDVEVEVC
    GZVKWCKVEVDWEVDVCVEVBVCURUGMABCBNOWAWBRVPHVCWBRKVBVEBPUHUIVBVEWDVPKZVCVBVEG
    VPEFWFACQVPUJSUKTULVCVGVMVQKZVBVGVCWGVGVCGZVMCCJIZVQHIZRVQHIZVQVGVCWEVMWJKV
    GVEVCVEVFUSUMCDCBNOWHWIRVQHVEWIRKVFVCCPUNUOVFVCWKVQKZVEVFVCGVQEFWLDBQVQUPSU
    QTMUQUTVA $.
    $( [18-May-2005] $)

