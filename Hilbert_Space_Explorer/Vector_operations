$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Vector operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Closure of vector addition. $)
  hvaddclt $p |- ( ( A e. H~ /\ B e. H~ ) -> ( A +h B ) e. H~ ) $=
    ( chil cva ax-hfvadd foprcl ) ABCCCDEF $.
    $( [18-Apr-2007] $) $( [18-Apr-2007] $)

  $( Closure of scalar multiplication. $)
  hvmulclt $p |- ( ( A e. CC /\ B e. H~ ) -> ( A .h B ) e. H~ ) $=
    ( chil cc csm ax-hfvmul foprcl ) ABCDCEFG $.
    $( [19-Apr-2007] $) $( [19-Apr-2007] $)

  ${
    hvmulcl.1 $e |- A e. CC $.
    hvmulcl.2 $e |- B e. H~ $.
    $( Closure inference for scalar multiplication. $)
    hvmulcl $p |- ( A .h B ) e. H~ $=
      ( cc wcel chil csm co hvmulclt mp2an ) AEFBGFABHIGFCDABJK $.
      $( [1-Aug-1999] $)
  $}

  ${
    $d x y z $.
    $( Define vector subtraction.  See ~ hvsubval for its value and
       ~ hvsubcl for its closure. $)
    df-hvsub $a |- -h = { <. <. x , y >. , z >. | ( ( x e. H~ /\ y e. H~ )
                   /\ z = ( x +h ( -u 1 .h y ) ) ) } $.

    $( Mapping domain and codomain of vector subtraction. $)
    hvsubopr $p |- -h : ( H~ X. H~ ) --> H~ $=
      ( vx vy vz chil cv c1 cneg csm co cva cmv df-hvsub wcel hvaddclt cc
      ax1cn negcl hvmulclt mpan sylan2 foprab ) ABCDDAEZFGZBEZHIZJIZDKABCLUBDMU
      EDMZUFDMUDDMZUBUENUCOMUHUGFPQUCUDRSTUA $.
      $( [19-Sep-2007] $) $( [6-Sep-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( Value of vector subtraction. $)
    hvsubvalt $p |- ( ( A e. H~ /\ B e. H~ ) ->
                    ( A -h B ) = ( A +h ( -u 1 .h B ) ) ) $=
      ( vx vy vz chil cv c1 cneg csm co cva cmv oprex opreq1 wceq opreq2
      opreq2d df-hvsub oprabval2 ) CDEABFFCGZHIZDGZJKZLKAUBBJKZLKMAUDLKAUELNUAA
      UDLOUCBPUDUEALUCBUBJQRCDEST $.
      $( [5-Sep-1999] $)
  $}

  $( Closure of vector subtraction. $)
  hvsubclt $p |- ( ( A e. H~ /\ B e. H~ ) -> ( A -h B ) e. H~ ) $=
    ( chil wcel wa cmv co c1 cneg csm cva hvsubvalt hvaddclt cc ax1cn negcl
    hvmulclt mpan sylan2 eqeltrd ) ACDZBCDZEABFGAHIZBJGZKGZCABLUAUDCDZUECDUBAUD
    MUCNDUBUFHOPUCBQRST $.
    $( [17-Aug-1999] $)

  ${
    hvaddcl.1 $e |- A e. H~ $.
    hvaddcl.2 $e |- B e. H~ $.
    $( Closure of vector addition. $)
    hvaddcl $p |- ( A +h B ) e. H~ $=
      ( chil wcel cva co hvaddclt mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [1-Aug-1999] $)

    $( Commutation of vector addition. $)
    hvcom $p |- ( A +h B ) = ( B +h A ) $=
      ( chil wcel cva co wceq ax-hvcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [3-Sep-1999] $)

    $( Value of vector subtraction definition. $)
    hvsubval $p |- ( A -h B ) = ( A +h ( -u 1 .h B ) ) $=
      ( chil wcel cmv co c1 cneg csm cva wceq hvsubvalt mp2an ) AEFBEFABGHAIJBK
      HLHMCDABNO $.
      $( [3-Sep-1999] $)

    $( Closure of vector subtraction. $)
    hvsubcl $p |- ( A -h B ) e. H~ $=
      ( chil wcel cmv co hvsubclt mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [2-Aug-1999] $)
  $}

  $( Addition with the zero vector. $)
  hvaddid2t $p |- ( A e. H~ -> ( 0h +h A ) = A ) $=
    ( chil wcel c0v cva co wceq ax-hv0cl ax-hvcom mpan2 ax-hvaddid eqtr3d ) ABC
    ZADEFZDAEFZAMDBCNOGHADIJAKL $.
    $( [18-Oct-1999] $)

  $( Scalar multiplication with the zero vector. $)
  hvmul0t $p |- ( A e. CC -> ( A .h 0h ) = 0h ) $=
    ( cc wcel c0v cc0 csm co cmul mul01t opreq1d chil wceq ax-hv0cl ax-hvmul0
    ax-mp syl6eq 0cn ax-hvmulass mp3an23 eqtr3d opreq2i syl6req ) ABCZDAEDFGZFG
    ZADFGUCAEHGZDFGZDUEUCUGUDDUCUFEDFAIJDKCZUDDLMDNOZPUCEBCUHUGUELQMAEDRSTUDDAF
    UIUAUB $.
    $( [30-May-1999] $)

  $( If a scalar product is zero, one of its factors must be zero. $)
  hvmul0ort $p |- ( ( A e. CC /\ B e. H~ ) ->
                  ( ( A .h B ) = 0h <-> ( A = 0 \/ B = 0h ) ) ) $=
    ( cc wcel chil wa csm co c0v wceq cc0 wo wne wn c1 cdiv opreq2 ad2antlr
    cmul recid2t opreq1d adantlr ax-hvmulass recclt simpll simplr syl3anc
    ax-hvmulid 3eqtr3d hvmul0t syl ex df-ne syl5ibr orrd wi opreq1 eqeq1d
    ax-hvmul0 syl5cbir adantl adantr jaod impbid ) ACDZBEDZFZABGHZIJZAKJZBIJZLZ
    VGVIVLVGVIFZVJVKVMAKMZVKVJNVMVNVKVMVNFOAPHZVHGHZVOIGHZBIVIVPVQJVGVNVHIVOGQR
    VGVNVPBJVIVGVNFZVOASHZBGHZOBGHZVPBVEVNVTWAJVFVEVNFZVSOBGATUAUBVOCDZVEVFVTVP
    JVRVOABUCVEVNWCVFAUDZUBVEVFVNUEVEVFVNUFUGVFWABJVEVNBUHRUIUBVGVNVQIJZVIVEVNW
    EVFWBWCWEWDVOUJUKUBUBUIULAKUMUNUOULVGVJVIVKVFVJVIUPVEVJVIKBGHZIJVFVJVHWFIAK
    BGUQURBUSUTVAVEVKVIUPVFVKVIAIGHZIJVEVKVHWGIBIAGQURAUJUTVBVCVD $.
    $( [21-May-2005] $) $( [19-May-2005] $)

  $( Subtraction of a vector from itself. $)
  hvsubidt $p |- ( A e. H~ -> ( A -h A ) = 0h ) $=
    ( chil wcel cmv co cc0 csm c0v c1 cneg caddc cva ax-hvmulid opreq1d cc
    wceq ax1cn negcl ax-hvdistr2 mp3an12 hvsubvalt anidms 3eqtr4rd negid
    opreq1i syl6eq ax-hvmul0 eqtrd ) ABCZAADEZFAGEZHUIUJIIJZKEZAGEZUKUIIAGEZULA
    GEZLEZAUPLEZUNUJUIUOAUPLAMNIOCULOCUIUNUQPQIQRIULASTUIUJURPAAUAUBUCUMFAGIQUD
    UEUFAUGUH $.
    $( [30-May-1999] $)

  $( Addition of negative of a vector to itself. $)
  hvnegidt $p |- ( A e. H~ -> ( A +h ( -u 1 .h A ) ) = 0h ) $=
    ( chil wcel cmv co c1 cneg csm cva c0v wceq hvsubvalt anidms hvsubidt
    eqtr3d ) ABCZAADEZAFGAHEIEZJPQRKAALMANO $.
    $( [3-Jun-2004] $) $( [4-Jun-2004] $)

  $( Two ways to express the negative of a vector. $)
  hv2negt $p |- ( A e. H~ -> ( 0h -h A ) = ( -u 1 .h A ) ) $=
    ( chil wcel c0v cmv co c1 cneg csm cva wceq ax-hv0cl hvsubvalt mpan cc
    ax1cn negcl hvmulclt hvaddid2t syl eqtrd ) ABCZDAEFZDGHZAIFZJFZUEDBCUBUCUFK
    LDAMNUBUEBCZUFUEKUDOCUBUGGPQUDARNUESTUA $.
    $( [24-May-2005] $) $( [23-May-2005] $)

  ${
    hvaddid2.1 $e |- A e. H~ $.
    $( Addition with the zero vector. $)
    hvaddid2 $p |- ( 0h +h A ) = A $=
      ( chil wcel c0v cva co wceq hvaddid2t ax-mp ) ACDEAFGAHBAIJ $.
      $( [18-Aug-1999] $)

    $( Addition of negative of a vector to itself. $)
    hvnegid $p |- ( A +h ( -u 1 .h A ) ) = 0h $=
      ( chil wcel c1 cneg csm co cva c0v wceq hvnegidt ax-mp ) ACDAEFAGHIHJKBAL
      M $.
      $( [18-Aug-1999] $)

    $( Two ways to express the negative of a vector. $)
    hv2neg $p |- ( 0h -h A ) = ( -u 1 .h A ) $=
      ( chil wcel c0v cmv co c1 cneg csm wceq hv2negt ax-mp ) ACDEAFGHIAJGKBALM
      $.
      $( [31-Oct-1999] $)
  $}

  $( Convert minus one times a scalar product to the negative of the scalar. $)
  hvm1negt $p |- ( ( A e. CC /\ B e. H~ ) ->
                 ( -u 1 .h ( A .h B ) ) = ( -u A .h B ) ) $=
    ( cc wcel chil wa c1 cneg cmul co csm wceq ax1cn negcl ax-hvmulass mp3an1
    mulm1t adantr opreq1d eqtr3d ) ACDZBEDZFZGHZAIJZBKJZUDABKJKJZAHZBKJUDCDUAUB
    UFUGLGMNUDABOPUCUEUHBKUAUEUHLUBAQRST $.
    $( [8-Jun-2004] $) $( [4-Jun-2004] $)

  $( Value of vector addition in terms of vector subtraction. $)
  hvaddsubvalt $p |- ( ( A e. H~ /\ B e. H~ ) ->
                  ( A +h B ) = ( A -h ( -u 1 .h B ) ) ) $=
    ( chil wcel wa c1 cneg csm co cmv cva wceq hvsubvalt cc ax1cn negcl
    hvmulclt mpan sylan2 hvm1negt negneg opreq1i syl6eq ax-hvmulid eqtrd
    adantl opreq2d eqtr2d ) ACDZBCDZEZAFGZBHIZJIZAULUMHIZKIZABKIUIUMCDZUNUPLUJA
    UMMULNDZUJUQFOPZULBQRSUKUOBAKUJUOBLUIUJUOFBHIZBUJUOULGZBHIZUTURUJUOVBLUSULB
    TRVAFBHFOUAUBUCBUDUEUFUGUH $.
    $( [19-Jun-2006] $) $( [10-Jun-2006] $)

  $( Commutative/associative law. $)
  hvadd23t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
               ( ( A +h B ) +h C ) = ( ( A +h C ) +h B ) ) $=
    ( chil wcel w3a cva co wceq wa ax-hvcom opreq2d 3adant1 ax-hvass 3com23
    3eqtr4d ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCK
    LMABCNQSRUDUCIACBNOP $.
    $( [16-Oct-1999] $)

  $( Commutative/associative law. $)
  hvadd12t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
               ( A +h ( B +h C ) ) = ( B +h ( A +h C ) ) ) $=
    ( chil wcel w3a cva co wceq wa ax-hvcom opreq1d 3adant3 ax-hvass 3com12
    3eqtr3d ) ADEZBDEZCDEZFABGHZCGHZBAGHZCGHZABCGHGHBACGHGHZQRUAUCISQRJTUBCGABK
    LMABCNRQSUCUDIBACNOP $.
    $( [19-Oct-1999] $)

  $( Hilbert vector space addition law. $)
  hvadd4t $p |- ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. H~ /\ D e. H~ ) ) ->
               ( ( A +h B ) +h ( C +h D ) ) = ( ( A +h C ) +h ( B +h D ) ) ) $=
    ( chil wcel wa cva co wceq w3a hvadd23t opreq1d 3expa adantrr ax-hvass
    3expb hvaddclt sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZ
    BHIZDHIZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUHEFZUGUJ
    UNJZUDUQUEUFURUHCDPQABRSUBUEUCUFUMUOJZUKEFZUCUFGUSUBUEGUTUCUFUSUKBDPQACRSTU
    A $.
    $( [16-Oct-1999] $)

  $( Hilbert vector space addition/subtraction law. $)
  hvsub4t $p |- ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. H~ /\ D e. H~ ) ) ->
               ( ( A +h B ) -h ( C +h D ) ) = ( ( A -h C ) +h ( B -h D ) ) ) $=
    ( chil wcel wa cva co c1 cneg csm cmv wceq cc ax1cn negcl ax-hvdistr1
    mp3an1 adantl opreq2d hvmulclt mpan anim2i anim12i an4s hvadd4t syl eqtr4d
    hvsubvalt hvaddclt syl2an ad2ant2r ad2ant2l opreq12d 3eqtr4d ) AEFZBEFZGZCE
    FZDEFZGZGZABHIZJKZCDHIZLIZHIZAVECLIZHIZBVEDLIZHIZHIZVDVFMIZACMIZBDMIZHIVCVH
    VDVIVKHIZHIZVMVCVGVQVDHVBVGVQNZUSVEOFZUTVAVSJPQZVECDRSTUAVCUQVIEFZGZURVKEFZ
    GZGZVMVRNUQUTURVAWFUQUTGWCURVAGWEUTWBUQVTUTWBWAVECUBUCUDVAWDURVTVAWDWAVEDUB
    UCUDUEUFAVIBVKUGUHUIVDEFVFEFVNVHNUSVBVDVFUJABUKCDUKULVCVOVJVPVLHUQUTVOVJNUR
    VAACUJUMURVAVPVLNUQUTBDUJUNUOUP $.
    $( [17-Oct-1999] $)

  $( Commutative/associative law. $)
  hvaddsub12t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
               ( A +h ( B -h C ) ) = ( B +h ( A -h C ) ) ) $=
    ( chil wcel w3a c1 cneg csm co cva cmv wceq hvadd12t cc ax1cn negcl
    hvmulclt mpan syl3an3 wa hvsubvalt opreq2d 3adant1 3adant2 3eqtr4d ) ADEZBD
    EZCDEZFABGHZCIJZKJZKJZBAUKKJZKJZABCLJZKJZBACLJZKJZUGUHUKDEZUMUOMUIABUKNUJOE
    UIUTGPQUJCRSTUHUIUQUMMUGUHUIUAUPULAKBCUBUCUDUGUIUSUOMUHUGUIUAURUNBKACUBUCUE
    UF $.
    $( [19-Oct-1999] $)

  $( Addition/subtraction cancellation law for vectors in Hilbert space. $)
  hvpncant $p |- ( ( A e. H~ /\ B e. H~ ) -> ( ( A +h B ) -h B ) = A ) $=
    ( chil wcel wa cva co cmv c1 cneg csm c0v wceq hvsubvalt hvaddclt pm3.27
    sylanc ax-hvass 3expb cc ax1cn negcl hvmulclt mpan ancli sylan2 eqtrd
    hvnegidt opreq2d adantl ax-hvaddid adantr 3eqtrd ) ACDZBCDZEZABFGZBHGZABIJZ
    BKGZFGZFGZALFGZAUPURUQUTFGZVBUQCDUOURVDMUPUQBNABOUNUOPQUNUOUTCDZEVDVBMZUOUN
    UOVEVFABUTRSUOVEUSTDUOVEIUAUBUSBUCUDUEUFUGUOVBVCMUNUOVALAFBUHUIUJUNVCAMUOAU
    KULUM $.
    $( [8-Jun-2004] $) $( [7-Jun-2004] $)

  $( Addition/subtraction cancellation law for vectors in Hilbert space. $)
  hvpncan2t $p |- ( ( A e. H~ /\ B e. H~ ) -> ( ( A +h B ) -h A ) = B ) $=
    ( chil wcel cva co cmv wceq wa ax-hvcom opreq1d hvpncant eqtr3d ancoms ) BC
    DZACDZABEFZAGFZBHOPIZBAEFZAGFRBSTQAGBAJKBALMN $.
    $( [8-Jun-2004] $) $( [7-Jun-2004] $)

  $( Associativity of sum and difference of Hilbert space vectors. $)
  hvaddsubasst $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) -> ( ( A +h B ) -h C )
                     = ( A +h ( B -h C ) ) ) $=
    ( chil wcel w3a cva co c1 cneg csm cmv wceq ax-hvass cc ax1cn negcl
    hvmulclt mpan syl3an3 wa hvsubvalt hvaddclt sylan 3impa 3adant1 opreq2d
    3eqtr4d ) ADEZBDEZCDEZFZABGHZIJZCKHZGHZABUOGHZGHZUMCLHZABCLHZGHUIUJUODEZUPU
    RMUKABUONUNOEUKVAIPQUNCRSTUIUJUKUSUPMZUMDEUKVBUIUJUAUMCUBABUCUDUEULUTUQAGUJ
    UKUTUQMUIBCUBUFUGUH $.
    $( [28-Aug-2004] $) $( [27-Aug-2004] $)

  $( Subtraction and addition of equal Hilbert space vectors.. $)
  hvpncan3t $p |- ( ( A e. H~ /\ B e. H~ ) -> ( A +h ( B -h A ) ) = B ) $=
    ( chil wcel wa cva co cmv wceq hvaddsubasst 3anidm13 hvpncan2t eqtr3d ) ACD
    ZBCDZEABFGAHGZABAHGFGZBNOPQIABAJKABLM $.
    $( [2-Sep-2004] $) $( [27-Aug-2004] $)

  $( Scalar multiplication commutative law. $)
  hvmulcomt $p |- ( ( A e. CC /\ B e. CC /\ C e. H~ ) ->
                 ( A .h ( B .h C ) ) = ( B .h ( A .h C ) ) ) $=
    ( cc wcel chil w3a cmul co csm wceq wa axmulcom opreq1d 3adant3
    ax-hvmulass 3com12 3eqtr3d ) ADEZBDEZCFEZGABHIZCJIZBAHIZCJIZABCJIJIBACJIJIZ
    STUCUEKUASTLUBUDCJABMNOABCPTSUAUEUFKBACPQR $.
    $( [19-May-2005] $) $( [19-May-2005] $)

  ${
    hvmulcom.1 $e |- A e. CC $.
    hvmulcom.2 $e |- B e. CC $.
    hvmulcom.3 $e |- C e. H~ $.
    $( Scalar multiplication associative law. $)
    hvmulass $p |- ( ( A x. B ) .h C ) = ( A .h ( B .h C ) ) $=
      ( cc wcel chil cmul co csm wceq ax-hvmulass mp3an ) AGHBGHCIHABJKCLKABCLK
      LKMDEFABCNO $.
      $( [3-Sep-1999] $)

    $( Scalar multiplication commutative law. $)
    hvmulcom $p |- ( A .h ( B .h C ) ) = ( B .h ( A .h C ) ) $=
      ( cc wcel chil csm co wceq hvmulcomt mp3an ) AGHBGHCIHABCJKJKBACJKJKLDEFA
      BCMN $.
      $( [3-Sep-1999] $)

    $( Double negative in scalar multiplication. $)
    hvmul2neg $p |- ( -u A .h ( -u B .h C ) ) = ( A .h ( B .h C ) ) $=
      ( cneg cmul co csm mul2neg opreq1i negcl hvmulass 3eqtr3 ) AGZBGZHIZCJIAB
      HIZCJIPQCJIJIABCJIJIRSCJABDEKLPQCADMBEMFNABCDEFNO $.
      $( [3-Sep-1999] $)
  $}

  $( Scalar multiplication distributive law for subtraction. $)
  hvsubdistr1t $p |- ( ( A e. CC /\ B e. H~ /\ C e. H~ ) ->
                    ( A .h ( B -h C ) ) = ( ( A .h B ) -h ( A .h C ) ) ) $=
    ( cc wcel chil w3a c1 cneg csm co cva cmv wceq ax-hvdistr1 ax1cn negcl
    hvmulclt mpan syl3an3 wa hvmulcomt mp3an2 opreq2d 3adant2 eqtrd hvsubvalt
    3adant1 3adant3 sylanc 3eqtr4d ) ADEZBFEZCFEZGZABHIZCJKZLKZJKZABJKZUPACJKZJ
    KZLKZABCMKZJKUTVAMKZUOUSUTAUQJKZLKZVCULUMUQFEZUSVGNUNABUQOUPDEZUNVHHPQZUPCR
    STULUNVGVCNUMULUNUAVFVBUTLULVIUNVFVBNVJAUPCUBUCUDUEUFUOVDURAJUMUNVDURNULBCU
    GUHUDUTFEZVAFEZVEVCNUOUTVAUGULUMVKUNABRUIULUNVLUMACRUEUJUK $.
    $( [19-May-2005] $) $( [19-May-2005] $)

  $( Scalar multiplication distributive law for subtraction. $)
  hvsubdistr2t $p |- ( ( A e. CC /\ B e. CC /\ C e. H~ ) -> ( ( A - B ) .h C
      ) = ( ( A .h C ) -h ( B .h C ) ) ) $=
    ( cc wcel chil w3a cmin co csm c1 cneg cva cmv caddc wceq ax-hvdistr2
    negclt syl3an2 negsubt 3adant3 opreq1d wa cmul mulm1t adantr ax1cn negcl
    ax-hvmulass mp3an1 eqtr3d 3adant1 opreq2d 3eqtr3d hvsubvalt hvmulclt
    3adant2 sylanc eqtr4d ) ADEZBDEZCFEZGZABHIZCJIZACJIZKLZBCJIZJIZMIZVFVHNIZVC
    ABLZOIZCJIZVFVLCJIZMIZVEVJUTVLDEVBVNVPPVAAVLCQBRSVCVMVDCJUTVAVMVDPVBABTUAUB
    VCVOVIVFMVAVBVOVIPUTVAVBUCVGBUDIZCJIZVOVIVAVRVOPVBVAVQVLCJBUEUBUFVGDEVAVBVR
    VIPKUGUHVGBCUIUJUKULUMUNVFFEZVHFEZVKVJPVCVFVHUOUTVBVSVAACUPUQVAVBVTUTBCUPUL
    URUS $.
    $( [1-Aug-2006] $) $( [19-May-2005] $)

  ${
    hvdistr1.1 $e |- A e. CC $.
    hvdistr1.2 $e |- B e. H~ $.
    hvdistr1.3 $e |- C e. H~ $.
    $( Scalar multiplication distributive law. $)
    hvdistr1 $p |- ( A .h ( B +h C ) ) = ( ( A .h B ) +h ( A .h C ) ) $=
      ( cc wcel chil cva co csm wceq ax-hvdistr1 mp3an ) AGHBIHCIHABCJKLKABLKAC
      LKJKMDEFABCNO $.
      $( [3-Sep-1999] $)

    $( Scalar multiplication distributive law. $)
    hvsubdistr1 $p |- ( A .h ( B -h C ) ) = ( ( A .h B ) -h ( A .h C ) ) $=
      ( cc wcel chil cmv co csm wceq hvsubdistr1t mp3an ) AGHBIHCIHABCJKLKABLKA
      CLKJKMDEFABCNO $.
      $( [3-Sep-1999] $)
  $}

  ${
    hvass.1 $e |- A e. H~ $.
    hvass.2 $e |- B e. H~ $.
    hvass.3 $e |- C e. H~ $.
    $( Hilbert vector space associative law. $)
    hvass $p |- ( ( A +h B ) +h C ) = ( A +h ( B +h C ) ) $=
      ( chil wcel cva co wceq ax-hvass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABCLM
      $.
      $( [3-Sep-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvadd23 $p |- ( ( A +h B ) +h C ) = ( ( A +h C ) +h B ) $=
      ( chil wcel cva co wceq hvadd23t mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM
      $.
      $( [18-Aug-1999] $)

    $( Hilbert vector space associative law for subtraction. $)
    hvsubass $p |- ( ( A -h B ) -h C ) = ( A -h ( B +h C ) ) $=
      ( cmv co c1 cneg csm cva hvsubcl hvsubval ax1cn negcl hvmulcl hvass
      hvdistr1 opreq2i eqtr4 opreq1i hvaddcl 3eqtr4 eqtr ) ABGHZCGHUFIJZCKHZLHZ
      ABCLHZGHZUFCABDEMFNAUGBKHZLHZUHLHZAUGUJKHZLHZUIUKUNAULUHLHZLHUPAULUHDUGBI
      OPZEQUGCURFQRUOUQALUGBCUREFSTUAUFUMUHLABDENUBAUJDBCEFUCNUDUE $.
      $( [7-Oct-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvsub23 $p |- ( ( A -h B ) -h C ) = ( ( A -h C ) -h B ) $=
      ( cva co cmv hvcom opreq2i hvsubass 3eqtr4 ) ABCGHZIHACBGHZIHABIHCIHACIHB
      IHNOAIBCEFJKABCDEFLACBDFELM $.
      $( [7-Oct-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvadd12 $p |- ( A +h ( B +h C ) ) = ( B +h ( A +h C ) ) $=
      ( cva co hvcom opreq1i hvass 3eqtr3 ) ABGHZCGHBAGHZCGHABCGHGHBACGHGHMNCGA
      BDEIJABCDEFKBACEDFKL $.
      $( [11-Sep-1999] $)

    ${
      hvadd4.4 $e |- D e. H~ $.
      $( Hilbert vector space addition law. $)
      hvadd4 $p |- ( ( A +h B ) +h ( C +h D ) ) =
                   ( ( A +h C ) +h ( B +h D ) ) $=
        ( chil wcel wa cva co wceq pm3.2i hvadd4t mp2an ) AIJZBIJZKCIJZDIJZKABL
        MCDLMLMACLMBDLMLMNRSEFOTUAGHOABCDPQ $.
        $( [3-Sep-1999] $)

      $( Hilbert vector space addition law. $)
      hvsubsub4 $p |- ( ( A -h B ) -h ( C -h D ) ) =
                      ( ( A -h C ) -h ( B -h D ) ) $=
        ( c1 cneg csm co cva cmv ax1cn negcl hvmulcl hvadd4 hvdistr1 opreq2i
        3eqtr4 hvaddcl hvsubval opreq12i ) AIJZBKLZMLZCUEDKLZMLZNLZAUECKLZMLZBU
        HMLZNLZABNLZCDNLZNLACNLZBDNLZNLUGUEUIKLZMLZULUEUMKLZMLZUJUNUGUKUEUHKLZM
        LZMLULUFVCMLZMLUTVBAUFUKVCEUEBIOPZFQZUECVFGQZUEUHVFUEDVFHQZQRUSVDUGMUEC
        UHVFGVISTVAVEULMUEBUHVFFVISTUAUGUIAUFEVGUBCUHGVIUBUCULUMAUKEVHUBBUHFVIU
        BUCUAUOUGUPUINABEFUCCDGHUCUDUQULURUMNACEGUCBDFHUCUDUA $.
        $( [31-Oct-1999] $)
    $}
  $}

  $( Hilbert vector space addition/subtraction law. $)
  hvsubsub4t $p |- ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. H~ /\ D e. H~ ) )
            -> ( ( A -h B ) -h ( C -h D ) ) = ( ( A -h C ) -h ( B -h D ) ) ) $=
    ( chil wcel cmv co wceq c0v cif opreq1 opreq1d eqeq12d opreq2 opreq2d
    ax-hv0cl elimel hvsubsub4 dedth4h ) AEFZBEFZCEFZDEFZABGHZCDGHZGHZACGHZBDGHZ
    GHZIABCDJJUAAJKZBGHZUFGHZUKCGHZUIGHZIUKUBBJKZGHZUFGHZUNUPDGHZGHZIUQUCCJKZDG
    HZGHZUKVAGHZUSGHZIUQVAUDDJKZGHZGHZVDUPVFGHZGHZIJJAUKIZUGUMUJUOVKUEULUFGAUKB
    GLMVKUHUNUIGAUKCGLMNBUPIZUMURUOUTVLULUQUFGBUPUKGOMVLUIUSUNGBUPDGLPNCVAIZURV
    CUTVEVMUFVBUQGCVADGLPVMUNVDUSGCVAUKGOMNDVFIZVCVHVEVJVNVBVGUQGDVFVAGOPVNUSVI
    VDGDVFUPGOPNUKUPVAVFAJEQRBJEQRCJEQRDJEQRST $.
    $( [2-Apr-2000] $)

  $( Two times a vector. $)
  hv2timest $p |- ( A e. H~ -> ( 2 .h A ) = ( A +h A ) ) $=
    ( chil wcel c2 csm co c1 cva caddc cc wceq ax1cn ax-hvdistr2 mp3an12 df-2
    opreq1i syl5eq ax-hvdistr1 mp3an1 anidms hvaddclt ax-hvmulid syl 3eqtr2d )
    ABCZDAEFZGAEFZUGHFZGAAHFZEFZUIUEGGIFZAEFZUHUFGJCZUMUEULUHKLLGGAMNDUKAEOPQUE
    UJUHKZUMUEUEUNLGAARSTUEUIBCZUJUIKUEUOAAUATUIUBUCUD $.
    $( [22-Jun-2006] $) $( [22-Jun-2006] $)

  ${
    hvnegdi.1 $e |- A e. H~ $.
    hvnegdi.2 $e |- B e. H~ $.
    $( Distribution of negative over subtraction. $)
    hvnegdi $p |- ( -u 1 .h ( A -h B ) ) = ( B -h A ) $=
      ( c1 cneg cmv co csm cva hvsubval opreq2i ax1cn negcl hvmulcl hvdistr1
      cmul mul2neg mulid1 eqtr opreq1i hvmulass chil wcel wceq ax-hvmulid
      ax-mp 3eqtr3 hvcom 3eqtr4 3eqtr ) EFZABGHZIHULAULBIHZJHZIHULAIHZULUNIHZJH
      ZBAGHZUMUOULIABCDKLULAUNEMNZCULBUTDOZPUQUPJHBUPJHURUSUQBUPJULULQHZBIHEBIH
      ZUQBVBEBIVBEEQHEEEMMREMSTUAULULBUTUTDUBBUCUDVCBUEDBUFUGUHUAUPUQULAUTCOULU
      NUTVAOUIBADCKUJUK $.
      $( [31-Oct-1999] $)

    $( If the difference between two vectors is zero, they are equal. $)
    hvsubeq0 $p |- ( ( A -h B ) = 0h <-> A = B ) $=
      ( cmv co c0v wceq c1 cneg csm cva hvsubval eqeq1i opreq1 sylbi ax1cn
      negcl hvmulcl hvadd23 hvass hvnegid opreq2i chil wcel ax-hvaddid ax-mp
      eqtr 3eqtr hvaddid2 3eqtr3g hvsubidt syl6eq impbi ) ABEFZGHZABHZUPAIJZBKF
      ZLFZBLFZGBLFZABUPUTGHVAVBHUOUTGABCDMNUTGBLOPVAABLFUSLFABUSLFZLFZAAUSBCURB
      IQRDSZDTABUSCDVEUAVDAGLFZAVCGALBDUBUCAUDUEVFAHCAUFUGUHUIBDUJUKUQUOBBEFZGA
      BBEOBUDUEVGGHDBULUGUMUN $.
      $( [18-Aug-1999] $)

    $( Vector cancellation law. $)
    hvsubcan2 $p |- ( ( A +h B ) +h ( A -h B ) ) = ( 2 .h A ) $=
      ( cva co cmv c1 cneg csm c2 c0v hvsubval opreq2i ax1cn negcl hvmulcl
      hvadd4 chil wcel wceq hv2timest ax-mp eqcomi hvnegid opreq12i eqtr 2cn
      ax-hvaddid 3eqtr ) ABEFZABGFZEFUKAHIZBJFZEFZEFZKAJFZLEFZUQULUOUKEABCDMNUP
      AAEFZBUNEFZEFURABAUNCDCUMBHOPDQRUSUQUTLEUQUSASTUQUSUACAUBUCUDBDUEUFUGUQST
      URUQUAKAUHCQUQUIUCUJ $.
      $( [3-Sep-1999] $)

    ${
      hvaddcan.3 $e |- C e. H~ $.
      $( Cancellation law for vector addition. $)
      hvaddcan $p |- ( ( A +h B ) = ( A +h C ) <-> B = C ) $=
        ( cva co wceq c1 cneg csm opreq1 c0v ax1cn negcl hvmulcl hvadd23
        hvnegid opreq1i hvaddid2 3eqtr 3eqtr3g opreq2 impbi ) ABGHZACGHZIZBCIUH
        UFJKZALHZGHZUGUJGHZBCUFUGUJGMUKAUJGHZBGHNBGHBABUJDEUIAJOPDQZRUMNBGADSZT
        BEUAUBULUMCGHNCGHCACUJDFUNRUMNCGUOTCFUAUBUCBCAGUDUE $.
        $( [11-Sep-1999] $)

      $( Relationship between vector subtraction and addition. $)
      hvsubadd $p |- ( ( A -h B ) = C <-> ( B +h C ) = A ) $=
        ( cmv co wceq c1 cneg csm cva hvsubval eqeq1i c0v ax1cn negcl hvmulcl
        hvadd12 hvnegid opreq2i chil wcel ax-hvaddid ax-mp 3eqtr hvaddcl
        hvaddcan eqcom 3bitr3 bitr ) ABGHZCIAJKZBLHZMHZCIZBCMHZAIZUMUPCABDENOBU
        PMHZURIAURIUQUSUTAURUTABUOMHZMHAPMHZABAUOEDUNBJQRESZTVAPAMBEUAUBAUCUDVB
        AIDAUEUFUGOBUPCEAUODVCUHFUIAURUJUKUL $.
        $( [11-Sep-1999] $)
    $}
  $}

  $( Distribution of negative over subtraction. $)
  hvnegdit $p |- ( ( A e. H~ /\ B e. H~ ) ->
                ( -u 1 .h ( A -h B ) ) = ( B -h A ) ) $=
    ( chil wcel c1 cneg cmv co csm wceq c0v cif opreq1 opreq2d opreq2 eqeq12d
    ax-hv0cl elimel hvnegdi dedth2h ) ACDZBCDZEFZABGHZIHZBAGHZJUCUAAKLZBGHZIHZB
    UGGHZJUCUGUBBKLZGHZIHZUKUGGHZJABKKAUGJZUEUIUFUJUOUDUHUCIAUGBGMNAUGBGOPBUKJZ
    UIUMUJUNUPUHULUCIBUKUGGONBUKUGGMPUGUKAKCQRBKCQRST $.
    $( [2-Apr-2000] $)

  $( If the difference between two vectors is zero, they are equal. $)
  hvsubeq0t $p |- ( ( A e. H~ /\ B e. H~ ) ->
                  ( ( A -h B ) = 0h <-> A = B ) ) $=
    ( chil wcel cmv co c0v wceq wb cif opreq1 eqeq1d eqeq1 bibi12d opreq2
    eqeq2 ax-hv0cl elimel hvsubeq0 dedth2h ) ACDZBCDZABEFZGHZABHZIUAAGJZBEFZGHZ
    UFBHZIUFUBBGJZEFZGHZUFUJHZIABGGAUFHZUDUHUEUIUNUCUGGAUFBEKLAUFBMNBUJHZUHULUI
    UMUOUGUKGBUJUFEOLBUJUFPNUFUJAGCQRBGCQRST $.
    $( [23-Oct-1999] $)

  $( If the sum of two vectors is zero, one is the negative of the other. $)
  hvaddeq0t $p |- ( ( A e. H~ /\ B e. H~ ) ->
                  ( ( A +h B ) = 0h <-> A = ( -u 1 .h B ) ) ) $=
    ( chil wcel wa cva co c0v wceq c1 cneg csm cmv hvaddsubvalt eqeq1d wb
    hvsubeq0t cc ax1cn negcl hvmulclt mpan sylan2 bitrd ) ACDZBCDZEZABFGZHIAJKZ
    BLGZMGZHIZAUJIZUGUHUKHABNOUEUJCDZULUMPUFAUJQUIRDUFUNJSTUIBUAUBUCUD $.
    $( [19-Jun-2006] $) $( [10-Jun-2006] $)

  $( Cancellation law for vector addition. $)
  hvaddcant $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                  ( ( A +h B ) = ( A +h C ) <-> B = C ) ) $=
    ( chil wcel cva co wceq wb c0v cif opreq1 eqeq12d bibi1d opreq2 eqeq1d
    eqeq1 bibi12d eqeq2d eqeq2 ax-hv0cl elimel hvaddcan dedth3h ) ADEZBDEZCDEZA
    BFGZACFGZHZBCHZIUEAJKZBFGZULCFGZHZUKIABCJJJULUFBJKZFGZUNHZUPCHZIUQULUGCJKZF
    GZHZUPUTHZIAULHZUJUOUKVDUHUMUIUNAULBFLAULCFLMNBUPHZUOURUKUSVEUMUQUNBUPULFOP
    BUPCQRCUTHZURVBUSVCVFUNVAUQCUTULFOSCUTUPTRULUPUTAJDUAUBBJDUAUBCJDUAUBUCUD
    $.
    $( [27-May-2005] $) $( [18-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvaddcan2t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                  ( ( A +h C ) = ( B +h C ) <-> A = B ) ) $=
    ( chil wcel cva co wceq wb w3a ax-hvcom 3adant3 3adant2 eqeq12d hvaddcant
    bitr3d 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIRSTJZCAFGZCBFGZHUCUDUEUFUAUGUBR
    SUFUAHTCAKLRTUGUBHSCBKMNCABOPQ $.
    $( [2-Jun-2005] $) $( [18-May-2005] $)

  $( Cancellation law for scalar multiplication. $)
  hvmulcant $p |- ( ( ( A e. CC /\ B e. H~ /\ C e. H~ ) /\ A =/= 0 ) ->
                  ( ( A .h B ) = ( A .h C ) <-> B = C ) ) $=
    ( cc wcel chil w3a cc0 wne wa cmv co c0v wceq csm wo wb wn df-ne biorf
    sylbi adantl hvmul0ort hvsubclt sylan2 3impb adantr bitr4d hvsubdistr1t
    eqeq1d hvsubeq0t hvmulclt 3adant3 3adant2 sylanc 3bitrd 3adant1 bitr3d ) AD
    EZBFEZCFEZGZAHIZJZBCKLZMNZABOLZACOLZNZBCNZVDVFAVEOLZMNZVGVHKLZMNZVIVDVFAHNZ
    VFPZVLVCVFVPQZVBVCVORVQAHSVOVFTUAUBVBVLVPQZVCUSUTVAVRUSVEFEVRUTVAJAVEUCBCUD
    UEUFUGUHVDVKVMMVBVKVMNVCABCUIUGUJVBVNVIQZVCVGFEZVHFEZVSVBVGVHUKUSUTVTVAABUL
    UMUSVAWAUTACULUNUOUGUPVBVFVJQZVCUTVAWBUSBCUKUQUGUR $.
    $( [22-May-2005] $) $( [19-May-2005] $)

  $( Cancellation law for scalar multiplication. $)
  hvmulcan2t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. H~ ) /\ C =/= 0h ) ->
                  ( ( A .h C ) = ( B .h C ) <-> A = B ) ) $=
    ( cc wcel chil w3a c0v wne wa csm co cmv wceq cmin cc0 wo wb wn df-ne
    biorf orcom syl6bb sylbi adantl hvmul0ort subclt sylan 3impa adantr
    hvsubdistr2t eqeq1d 3bitr2rd hvsubeq0t hvmulclt 3adant2 3adant1 sylanc
    subeq0t 3adant3 3bitr3d ) ADEZBDEZCFEZGZCHIZJZACKLZBCKLZMLZHNZABOLZPNZVHVIN
    ZABNZVGVMVMCHNZQZVLCKLZHNZVKVFVMVQRZVEVFVPSZVTCHTWAVMVPVMQVQVPVMUAVPVMUBUCU
    DUEVEVSVQRZVFVBVCVDWBVLDEVDWBVBVCJVLCUFABUGUHUIUJVGVRVJHVEVRVJNVFABCUKUJULU
    MVEVKVNRZVFVHFEZVIFEZWCVEVHVIUNVBVDWDVCACUOUPVCVDWEVBBCUOUQURUJVEVMVORZVFVB
    VCWFVDABUSUTUJVA $.
    $( [23-Jun-2005] $) $( [19-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvsubcant $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                  ( ( A -h B ) = ( A -h C ) <-> B = C ) ) $=
    ( chil wcel w3a cmv co wceq c1 cneg csm cva hvsubvalt 3adant3 3adant2
    eqeq12d wb hvaddcant cc ax1cn negcl hvmulclt mpan syl3an3 syl3an2 cc0 wne
    ax1ne0 negn0 hvmulcant mpan2 mp3an1 3adant1 3bitrd ) ADEZBDEZCDEZFZABGHZACG
    HZIAJKZBLHZMHZAVBCLHZMHZIZVCVEIZBCIZUSUTVDVAVFUPUQUTVDIURABNOUPURVAVFIUQACN
    PQUPVCDEZURVGVHRZUQUPVJVEDEZVKURAVCVESVBTEZURVLJUAUBZVBCUCUDUEVMUQVJVNVBBUC
    UDUFUQURVHVIRZUPVMUQURVOVNVMUQURFVBUGUHVOJUAUIUJVBBCUKULUMUNUO $.
    $( [5-Jun-2005] $) $( [18-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvsubcan2t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                  ( ( A -h C ) = ( B -h C ) <-> A = B ) ) $=
    ( chil wcel cmv co wceq wb w3a c1 cneg csm cc ax1cn negcl cc0 wne ax1ne0
    negn0 hvmulcant mpan2 mp3an1 hvsubclt 3adant3 3adant2 sylanc hvnegdit
    eqeq12d hvsubcant 3bitr3d 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIUMUNUOJZKLZC
    AFGZMGZVACBFGZMGZHZVBVDHZURUSVBDEZVDDEZVFVGIZUTVANEZVHVIVJKOPVKVHVIJVAQRVJK
    OSTVAVBVDUAUBUCUMUNVHUOCAUDUEUMUOVIUNCBUDUFUGUTVCUPVEUQUMUNVCUPHUOCAUHUEUMU
    OVEUQHUNCBUHUFUICABUJUKUL $.
    $( [26-Jun-2005] $) $( [18-May-2005] $)

  $( Subtraction of a zero vector. $)
  hvsub0t $p |- ( A e. H~ -> ( A -h 0h ) = A ) $=
    ( chil wcel c0v cmv co cva c1 cneg csm wceq ax-hv0cl hvsubvalt mpan2 cc
    ax1cn negcl hvmul0t ax-mp opreq2i syl6eq ax-hvaddid eqtrd ) ABCZADEFZADGFZA
    UDUEAHIZDJFZGFZUFUDDBCUEUIKLADMNUHDAGUGOCUHDKHPQUGRSTUAAUBUC $.
    $( [2-Apr-2000] $)

  $( Relationship between vector subtraction and addition. $)
  hvsubaddt $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                  ( ( A -h B ) = C <-> ( B +h C ) = A ) ) $=
    ( chil wcel cmv co wceq cva wb c0v cif opreq1 eqeq1d eqeq2 bibi12d opreq2
    ax-hv0cl elimel hvsubadd dedth3h ) ADEZBDEZCDEZABFGZCHZBCIGZAHZJUBAKLZBFGZC
    HZUGUIHZJABCKKKUIUCBKLZFGZCHZUMCIGZUIHZJUNUDCKLZHZUMURIGZUIHZJAUIHZUFUKUHUL
    VBUEUJCAUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFQNVCUGUPUIBUMCIMNPCURHZUOUSU
    QVACURUNOVDUPUTUICURUMIQNPUIUMURAKDRSBKDRSCKDRSTUA $.
    $( [30-Oct-1999] $)

  $( Hilbert vector space addition/subtraction law. $)
  hvaddsub4t $p |- ( ( ( A e. H~ /\ B e. H~ ) /\ ( C e. H~ /\ D e. H~ ) ) ->
          ( ( A +h B ) = ( C +h D ) <-> ( A -h C ) = ( D -h B ) ) ) $=
    ( chil wcel wa cva co cmv wceq wb hvsubcan2t hvaddclt adantr adantl ancoms
    ad2ant2lr syl3anc c0v pm3.27 anim2i hvsub4t syldan hvsubidt ad2antlr
    opreq2d hvsubclt ax-hvaddid syl adantlr 3eqtrd adantrr pm3.26 anim1i
    ad2antrr opreq1d hvaddid2t adantll eqeq12d bitr3d ) AEFZBEFZGZCEFZDEFZGZGZA
    BHIZCBHIZJIZCDHIZVJJIZKZVIVLKZACJIZDBJIZKVIEFZVLEFZVJEFZVNVOLVHVIVLVJMVDVRV
    GABNOVGVSVDCDNPVCVEVTVBVFVEVCVTCBNQRSVHVKVPVMVQVDVEVKVPKVFVDVEGZVKVPBBJIZHI
    ZVPTHIZVPVDVEVEVCGZVKWCKVEVDWEVDVCVEVBVCUAUBQABCBUCUDWAWBTVPHVCWBTKVBVEBUEU
    FUGVBVEWDVPKZVCVBVEGVPEFWFACUHVPUIUJUKULUMVCVGVMVQKZVBVGVCWGVGVCGZVMCCJIZVQ
    HIZTVQHIZVQVGVCWEVMWJKVGVEVCVEVFUNUOCDCBUCUDWHWITVQHVEWITKVFVCCUEUPUQVFVCWK
    VQKZVEVFVCGVQEFWLDBUHVQURUJUSULQUSUTVA $.
    $( [30-Jun-2005] $) $( [18-May-2005] $)

