$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Vector operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The Hilbert space scalar product operation is a set. $)
  hvmulex $p |- .h e. _V $=
    ( cc chil cxp csm wf cvv wcel ax-hfvmul axcnex ax-hilex xpex fex mp2an ) AB
    CZBDENFGDFGHABIJKNBFDLM $.
    $( [22-Apr-2007] $) $( [17-Apr-2007] $)

  $( Closure of vector addition. $)
  hvaddcl $p |- ( ( A e. ~H /\ B e. ~H ) -> ( A +h B ) e. ~H ) $=
    ( chil cva ax-hfvadd foprcl ) ABCCCDEF $.
    $( [18-Apr-2007] $) $( [18-Apr-2007] $)

  $( Closure of scalar multiplication. $)
  hvmulcl $p |- ( ( A e. CC /\ B e. ~H ) -> ( A .h B ) e. ~H ) $=
    ( chil cc csm ax-hfvmul foprcl ) ABCDCEFG $.
    $( [19-Apr-2007] $) $( [19-Apr-2007] $)

  ${
    hvmulcl.1 $e |- A e. CC $.
    hvmulcl.2 $e |- B e. ~H $.
    $( Closure inference for scalar multiplication. $)
    hvmulcli $p |- ( A .h B ) e. ~H $=
      ( cc wcel chil csm co hvmulcl mp2an ) AEFBGFABHIGFCDABJK $.
      $( [1-Aug-1999] $)
  $}

  ${
    $d x y z $.
    $( Mapping domain and codomain of vector subtraction. $)
    hvsubopr $p |- -h : ( ~H X. ~H ) --> ~H $=
      ( vx vy vz chil cv c1 cneg csm co cva cmv df-hvsub wcel hvaddcl cc ax1cn
      negcli hvmulcl mpan sylan2 foprab ) ABCDDAEZFGZBEZHIZJIZDKABCLUBDMUEDMZUF
      DMUDDMZUBUENUCOMUHUGFPQUCUDRSTUA $.
      $( [19-Sep-2007] $) $( [6-Sep-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( Value of vector subtraction. $)
    hvsubval $p |- ( ( A e. ~H /\ B e. ~H ) ->
                    ( A -h B ) = ( A +h ( -u 1 .h B ) ) ) $=
      ( vx vy vz chil cv c1 cneg csm co cva cmv oprex opreq1 wceq opreq2
      opreq2d df-hvsub oprabval2 ) CDEABFFCGZHIZDGZJKZLKAUBBJKZLKMAUDLKAUELNUAA
      UDLOUCBPUDUEALUCBUBJQRCDEST $.
      $( [5-Sep-1999] $)
  $}

  $( Closure of vector subtraction. $)
  hvsubcl $p |- ( ( A e. ~H /\ B e. ~H ) -> ( A -h B ) e. ~H ) $=
    ( chil wcel wa cmv co c1 cneg csm cva hvsubval hvaddcl cc ax1cn negcli
    hvmulcl mpan sylan2 eqeltrd ) ACDZBCDZEABFGAHIZBJGZKGZCABLUAUDCDZUECDUBAUDM
    UCNDUBUFHOPUCBQRST $.
    $( [17-Aug-1999] $)

  ${
    hvaddcl.1 $e |- A e. ~H $.
    hvaddcl.2 $e |- B e. ~H $.
    $( Closure of vector addition. $)
    hvaddcli $p |- ( A +h B ) e. ~H $=
      ( chil wcel cva co hvaddcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [1-Aug-1999] $)

    $( Commutation of vector addition. $)
    hvcomi $p |- ( A +h B ) = ( B +h A ) $=
      ( chil wcel cva co wceq ax-hvcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [3-Sep-1999] $)

    $( Value of vector subtraction definition. $)
    hvsubvali $p |- ( A -h B ) = ( A +h ( -u 1 .h B ) ) $=
      ( chil wcel cmv co c1 cneg csm cva wceq hvsubval mp2an ) AEFBEFABGHAIJBKH
      LHMCDABNO $.
      $( [3-Sep-1999] $)

    $( Closure of vector subtraction. $)
    hvsubcli $p |- ( A -h B ) e. ~H $=
      ( chil wcel cmv co hvsubcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [2-Aug-1999] $)
  $}

  $( Addition with the zero vector. $)
  hvaddid2 $p |- ( A e. ~H -> ( 0h +h A ) = A ) $=
    ( chil wcel c0v cva co wceq ax-hv0cl ax-hvcom mpan2 ax-hvaddid eqtr3d ) ABC
    ZADEFZDAEFZAMDBCNOGHADIJAKL $.
    $( [18-Oct-1999] $)

  $( Scalar multiplication with the zero vector. $)
  hvmul0 $p |- ( A e. CC -> ( A .h 0h ) = 0h ) $=
    ( cc wcel c0v cc0 csm co cmul mul01 opreq1d chil wceq ax-hv0cl ax-hvmul0
    ax-mp syl6eq 0cn ax-hvmulass mp3an23 eqtr3d opreq2i syl6req ) ABCZDAEDFGZFG
    ZADFGUCAEHGZDFGZDUEUCUGUDDUCUFEDFAIJDKCZUDDLMDNOZPUCEBCUHUGUELQMAEDRSTUDDAF
    UIUAUB $.
    $( [30-May-1999] $)

  $( If a scalar product is zero, one of its factors must be zero. $)
  hvmul0or $p |- ( ( A e. CC /\ B e. ~H ) ->
                  ( ( A .h B ) = 0h <-> ( A = 0 \/ B = 0h ) ) ) $=
    ( cc wcel chil wa csm co c0v wceq cc0 wo wne wn c1 cdiv opreq2 ad2antlr
    cmul recid2 opreq1d adantlr ax-hvmulass reccl simpll simplr syl3anc
    ax-hvmulid 3eqtr3d hvmul0 syl ex df-ne syl5ibr orrd wi opreq1 eqeq1d
    ax-hvmul0 syl5cbir adantl adantr jaod impbid ) ACDZBEDZFZABGHZIJZAKJZBIJZLZ
    VGVIVLVGVIFZVJVKVMAKMZVKVJNVMVNVKVMVNFOAPHZVHGHZVOIGHZBIVIVPVQJVGVNVHIVOGQR
    VGVNVPBJVIVGVNFZVOASHZBGHZOBGHZVPBVEVNVTWAJVFVEVNFZVSOBGATUAUBVOCDZVEVFVTVP
    JVRVOABUCVEVNWCVFAUDZUBVEVFVNUEVEVFVNUFUGVFWABJVEVNBUHRUIUBVGVNVQIJZVIVEVNW
    EVFWBWCWEWDVOUJUKUBUBUIULAKUMUNUOULVGVJVIVKVFVJVIUPVEVJVIKBGHZIJVFVJVHWFIAK
    BGUQURBUSUTVAVEVKVIUPVFVKVIAIGHZIJVEVKVHWGIBIAGQURAUJUTVBVCVD $.
    $( [21-May-2005] $) $( [19-May-2005] $)

  $( Subtraction of a vector from itself. $)
  hvsubid $p |- ( A e. ~H -> ( A -h A ) = 0h ) $=
    ( chil wcel cmv co cc0 csm c0v c1 cneg caddc cva ax-hvmulid opreq1d cc
    wceq ax1cn negcli ax-hvdistr2 mp3an12 hvsubval anidms 3eqtr4rd negidi
    opreq1i syl6eq ax-hvmul0 eqtrd ) ABCZAADEZFAGEZHUIUJIIJZKEZAGEZUKUIIAGEZULA
    GEZLEZAUPLEZUNUJUIUOAUPLAMNIOCULOCUIUNUQPQIQRIULASTUIUJURPAAUAUBUCUMFAGIQUD
    UEUFAUGUH $.
    $( [30-May-1999] $)

  $( Addition of negative of a vector to itself. $)
  hvnegid $p |- ( A e. ~H -> ( A +h ( -u 1 .h A ) ) = 0h ) $=
    ( chil wcel cmv co c1 cneg csm cva c0v wceq hvsubval anidms hvsubid eqtr3d
    ) ABCZAADEZAFGAHEIEZJPQRKAALMANO $.
    $( [3-Jun-2004] $) $( [4-Jun-2004] $)

  $( Two ways to express the negative of a vector. $)
  hv2neg $p |- ( A e. ~H -> ( 0h -h A ) = ( -u 1 .h A ) ) $=
    ( chil wcel c0v cmv co c1 cneg csm cva wceq ax-hv0cl hvsubval mpan cc
    ax1cn negcli hvmulcl hvaddid2 syl eqtrd ) ABCZDAEFZDGHZAIFZJFZUEDBCUBUCUFKL
    DAMNUBUEBCZUFUEKUDOCUBUGGPQUDARNUESTUA $.
    $( [24-May-2005] $) $( [23-May-2005] $)

  ${
    hvaddid2.1 $e |- A e. ~H $.
    $( Addition with the zero vector. $)
    hvaddid2i $p |- ( 0h +h A ) = A $=
      ( chil wcel c0v cva co wceq hvaddid2 ax-mp ) ACDEAFGAHBAIJ $.
      $( [18-Aug-1999] $)

    $( Addition of negative of a vector to itself. $)
    hvnegidi $p |- ( A +h ( -u 1 .h A ) ) = 0h $=
      ( chil wcel c1 cneg csm co cva c0v wceq hvnegid ax-mp ) ACDAEFAGHIHJKBALM
      $.
      $( [18-Aug-1999] $)

    $( Two ways to express the negative of a vector. $)
    hv2negi $p |- ( 0h -h A ) = ( -u 1 .h A ) $=
      ( chil wcel c0v cmv co c1 cneg csm wceq hv2neg ax-mp ) ACDEAFGHIAJGKBALM
      $.
      $( [31-Oct-1999] $)
  $}

  $( Convert minus one times a scalar product to the negative of the scalar. $)
  hvm1neg $p |- ( ( A e. CC /\ B e. ~H ) ->
                 ( -u 1 .h ( A .h B ) ) = ( -u A .h B ) ) $=
    ( cc wcel chil wa c1 cneg cmul co csm wceq ax1cn negcli ax-hvmulass mp3an1
    mulm1 adantr opreq1d eqtr3d ) ACDZBEDZFZGHZAIJZBKJZUDABKJKJZAHZBKJUDCDUAUBU
    FUGLGMNUDABOPUCUEUHBKUAUEUHLUBAQRST $.
    $( [8-Jun-2004] $) $( [4-Jun-2004] $)

  $( Value of vector addition in terms of vector subtraction. $)
  hvaddsubval $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( A +h B ) = ( A -h ( -u 1 .h B ) ) ) $=
    ( chil wcel wa c1 cneg csm co cmv cva wceq hvsubval cc ax1cn negcli
    hvmulcl mpan sylan2 hvm1neg negnegi opreq1i syl6eq ax-hvmulid eqtrd adantl
    opreq2d eqtr2d ) ACDZBCDZEZAFGZBHIZJIZAULUMHIZKIZABKIUIUMCDZUNUPLUJAUMMULND
    ZUJUQFOPZULBQRSUKUOBAKUJUOBLUIUJUOFBHIZBUJUOULGZBHIZUTURUJUOVBLUSULBTRVAFBH
    FOUAUBUCBUDUEUFUGUH $.
    $( [19-Jun-2006] $) $( [10-Jun-2006] $)

  $( Commutative/associative law. $)
  hvadd23 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
               ( ( A +h B ) +h C ) = ( ( A +h C ) +h B ) ) $=
    ( chil wcel w3a cva co wceq wa ax-hvcom opreq2d 3adant1 ax-hvass 3com23
    3eqtr4d ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCK
    LMABCNQSRUDUCIACBNOP $.
    $( [16-Oct-1999] $)

  $( Commutative/associative law. $)
  hvadd12 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
               ( A +h ( B +h C ) ) = ( B +h ( A +h C ) ) ) $=
    ( chil wcel w3a cva co wceq wa ax-hvcom opreq1d 3adant3 ax-hvass 3com12
    3eqtr3d ) ADEZBDEZCDEZFABGHZCGHZBAGHZCGHZABCGHGHBACGHGHZQRUAUCISQRJTUBCGABK
    LMABCNRQSUCUDIBACNOP $.
    $( [19-Oct-1999] $)

  $( Hilbert vector space addition law. $)
  hvadd4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
               ( ( A +h B ) +h ( C +h D ) ) = ( ( A +h C ) +h ( B +h D ) ) ) $=
    ( chil wcel wa cva co wceq w3a hvadd23 opreq1d 3expa adantrr ax-hvass
    3expb hvaddcl sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZB
    HIZDHIZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUHEFZUGUJU
    NJZUDUQUEUFURUHCDPQABRSUBUEUCUFUMUOJZUKEFZUCUFGUSUBUEGUTUCUFUSUKBDPQACRSTUA
    $.
    $( [16-Oct-1999] $)

  $( Hilbert vector space addition/subtraction law. $)
  hvsub4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
               ( ( A +h B ) -h ( C +h D ) ) = ( ( A -h C ) +h ( B -h D ) ) ) $=
    ( chil wcel wa cva co c1 cneg csm cmv wceq cc ax1cn negcli ax-hvdistr1
    mp3an1 adantl opreq2d hvmulcl mpan anim2i anim12i an4s hvadd4 syl eqtr4d
    hvsubval hvaddcl syl2an ad2ant2r ad2ant2l opreq12d 3eqtr4d ) AEFZBEFZGZCEFZ
    DEFZGZGZABHIZJKZCDHIZLIZHIZAVECLIZHIZBVEDLIZHIZHIZVDVFMIZACMIZBDMIZHIVCVHVD
    VIVKHIZHIZVMVCVGVQVDHVBVGVQNZUSVEOFZUTVAVSJPQZVECDRSTUAVCUQVIEFZGZURVKEFZGZ
    GZVMVRNUQUTURVAWFUQUTGWCURVAGWEUTWBUQVTUTWBWAVECUBUCUDVAWDURVTVAWDWAVEDUBUC
    UDUEUFAVIBVKUGUHUIVDEFVFEFVNVHNUSVBVDVFUJABUKCDUKULVCVOVJVPVLHUQUTVOVJNURVA
    ACUJUMURVAVPVLNUQUTBDUJUNUOUP $.
    $( [17-Oct-1999] $)

  $( Commutative/associative law. $)
  hvaddsub12 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
               ( A +h ( B -h C ) ) = ( B +h ( A -h C ) ) ) $=
    ( chil wcel w3a c1 cneg csm co cva cmv wceq hvadd12 cc ax1cn negcli
    hvmulcl mpan syl3an3 wa hvsubval opreq2d 3adant1 3adant2 3eqtr4d ) ADEZBDEZ
    CDEZFABGHZCIJZKJZKJZBAUKKJZKJZABCLJZKJZBACLJZKJZUGUHUKDEZUMUOMUIABUKNUJOEUI
    UTGPQUJCRSTUHUIUQUMMUGUHUIUAUPULAKBCUBUCUDUGUIUSUOMUHUGUIUAURUNBKACUBUCUEUF
    $.
    $( [19-Oct-1999] $)

  $( Addition/subtraction cancellation law for vectors in Hilbert space. $)
  hvpncan $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( A +h B ) -h B ) = A ) $=
    ( chil wcel wa cva co cmv c1 cneg csm c0v wceq hvaddcl hvsubval sylancom
    ax-hvass 3expb cc ax1cn negcli hvmulcl mpan ancli sylan2 eqtrd hvnegid
    opreq2d adantl ax-hvaddid adantr 3eqtrd ) ACDZBCDZEZABFGZBHGZABIJZBKGZFGZFG
    ZALFGZAUOUQUPUSFGZVAUMUNUPCDUQVCMABNUPBOPUMUNUSCDZEVCVAMZUNUMUNVDVEABUSQRUN
    VDURSDUNVDITUAURBUBUCUDUEUFUNVAVBMUMUNUTLAFBUGUHUIUMVBAMUNAUJUKUL $.
    $( [8-Jun-2004] $) $( [7-Jun-2004] $)

  $( Addition/subtraction cancellation law for vectors in Hilbert space. $)
  hvpncan2 $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( A +h B ) -h A ) = B ) $=
    ( chil wcel cva co cmv wceq wa ax-hvcom opreq1d hvpncan eqtr3d ancoms ) BCD
    ZACDZABEFZAGFZBHOPIZBAEFZAGFRBSTQAGBAJKBALMN $.
    $( [8-Jun-2004] $) $( [7-Jun-2004] $)

  $( Associativity of sum and difference of Hilbert space vectors. $)
  hvaddsubass $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) -> ( ( A +h B ) -h C )
                     = ( A +h ( B -h C ) ) ) $=
    ( chil wcel w3a cva co c1 cneg csm cmv wceq ax-hvass cc ax1cn negcli
    hvmulcl mpan syl3an3 wa hvsubval hvaddcl sylan 3impa 3adant1 opreq2d
    3eqtr4d ) ADEZBDEZCDEZFZABGHZIJZCKHZGHZABUOGHZGHZUMCLHZABCLHZGHUIUJUODEZUPU
    RMUKABUONUNOEUKVAIPQUNCRSTUIUJUKUSUPMZUMDEUKVBUIUJUAUMCUBABUCUDUEULUTUQAGUJ
    UKUTUQMUIBCUBUFUGUH $.
    $( [28-Aug-2004] $) $( [27-Aug-2004] $)

  $( Subtraction and addition of equal Hilbert space vectors.. $)
  hvpncan3 $p |- ( ( A e. ~H /\ B e. ~H ) -> ( A +h ( B -h A ) ) = B ) $=
    ( chil wcel wa cva co cmv wceq hvaddsubass 3anidm13 hvpncan2 eqtr3d ) ACDZB
    CDZEABFGAHGZABAHGFGZBNOPQIABAJKABLM $.
    $( [2-Sep-2004] $) $( [27-Aug-2004] $)

  $( Scalar multiplication commutative law. $)
  hvmulcom $p |- ( ( A e. CC /\ B e. CC /\ C e. ~H ) ->
                 ( A .h ( B .h C ) ) = ( B .h ( A .h C ) ) ) $=
    ( cc wcel chil w3a cmul co csm wceq wa mulcom opreq1d 3adant3 ax-hvmulass
    3com12 3eqtr3d ) ADEZBDEZCFEZGABHIZCJIZBAHIZCJIZABCJIJIBACJIJIZSTUCUEKUASTL
    UBUDCJABMNOABCPTSUAUEUFKBACPQR $.
    $( [19-May-2005] $) $( [19-May-2005] $)

  ${
    hvmulcom.1 $e |- A e. CC $.
    hvmulcom.2 $e |- B e. CC $.
    hvmulcom.3 $e |- C e. ~H $.
    $( Scalar multiplication associative law. $)
    hvmulassi $p |- ( ( A x. B ) .h C ) = ( A .h ( B .h C ) ) $=
      ( cc wcel chil cmul co csm wceq ax-hvmulass mp3an ) AGHBGHCIHABJKCLKABCLK
      LKMDEFABCNO $.
      $( [3-Sep-1999] $)

    $( Scalar multiplication commutative law. $)
    hvmulcomi $p |- ( A .h ( B .h C ) ) = ( B .h ( A .h C ) ) $=
      ( cc wcel chil csm co wceq hvmulcom mp3an ) AGHBGHCIHABCJKJKBACJKJKLDEFAB
      CMN $.
      $( [3-Sep-1999] $)

    $( Double negative in scalar multiplication. $)
    hvmul2negi $p |- ( -u A .h ( -u B .h C ) ) = ( A .h ( B .h C ) ) $=
      ( cneg cmul co csm mul2negi opreq1i negcli hvmulassi 3eqtr3i ) AGZBGZHIZC
      JIABHIZCJIPQCJIJIABCJIJIRSCJABDEKLPQCADMBEMFNABCDEFNO $.
      $( [3-Sep-1999] $)
  $}

  $( Scalar multiplication distributive law for subtraction. $)
  hvsubdistr1 $p |- ( ( A e. CC /\ B e. ~H /\ C e. ~H ) ->
                    ( A .h ( B -h C ) ) = ( ( A .h B ) -h ( A .h C ) ) ) $=
    ( cc wcel chil w3a c1 cneg csm co cva cmv wceq ax-hvdistr1 ax1cn negcli
    hvmulcl mpan syl3an3 wa hvmulcom mp3an2 opreq2d 3adant2 eqtrd hvsubval
    3adant1 3adant3 sylanc 3eqtr4d ) ADEZBFEZCFEZGZABHIZCJKZLKZJKZABJKZUPACJKZJ
    KZLKZABCMKZJKUTVAMKZUOUSUTAUQJKZLKZVCULUMUQFEZUSVGNUNABUQOUPDEZUNVHHPQZUPCR
    STULUNVGVCNUMULUNUAVFVBUTLULVIUNVFVBNVJAUPCUBUCUDUEUFUOVDURAJUMUNVDURNULBCU
    GUHUDUTFEZVAFEZVEVCNUOUTVAUGULUMVKUNABRUIULUNVLUMACRUEUJUK $.
    $( [19-May-2005] $) $( [19-May-2005] $)

  $( Scalar multiplication distributive law for subtraction. $)
  hvsubdistr2 $p |- ( ( A e. CC /\ B e. CC /\ C e. ~H ) -> ( ( A - B ) .h C
      ) = ( ( A .h C ) -h ( B .h C ) ) ) $=
    ( cc wcel chil w3a cmin co csm c1 cneg cva cmv caddc wceq ax-hvdistr2
    negcl syl3an2 negsub 3adant3 opreq1d wa cmul mulm1 adantr ax1cn negcli
    ax-hvmulass mp3an1 eqtr3d 3adant1 opreq2d 3eqtr3d hvsubval hvmulcl 3adant2
    sylanc eqtr4d ) ADEZBDEZCFEZGZABHIZCJIZACJIZKLZBCJIZJIZMIZVFVHNIZVCABLZOIZC
    JIZVFVLCJIZMIZVEVJUTVLDEVBVNVPPVAAVLCQBRSVCVMVDCJUTVAVMVDPVBABTUAUBVCVOVIVF
    MVAVBVOVIPUTVAVBUCVGBUDIZCJIZVOVIVAVRVOPVBVAVQVLCJBUEUBUFVGDEVAVBVRVIPKUGUH
    VGBCUIUJUKULUMUNVFFEZVHFEZVKVJPVCVFVHUOUTVBVSVAACUPUQVAVBVTUTBCUPULURUS $.
    $( [1-Aug-2006] $) $( [19-May-2005] $)

  ${
    hvdistr1.1 $e |- A e. CC $.
    hvdistr1.2 $e |- B e. ~H $.
    hvdistr1.3 $e |- C e. ~H $.
    $( Scalar multiplication distributive law. $)
    hvdistr1i $p |- ( A .h ( B +h C ) ) = ( ( A .h B ) +h ( A .h C ) ) $=
      ( cc wcel chil cva co csm wceq ax-hvdistr1 mp3an ) AGHBIHCIHABCJKLKABLKAC
      LKJKMDEFABCNO $.
      $( [3-Sep-1999] $)

    $( Scalar multiplication distributive law. $)
    hvsubdistr1i $p |- ( A .h ( B -h C ) ) = ( ( A .h B ) -h ( A .h C ) ) $=
      ( cc wcel chil cmv co csm wceq hvsubdistr1 mp3an ) AGHBIHCIHABCJKLKABLKAC
      LKJKMDEFABCNO $.
      $( [3-Sep-1999] $)
  $}

  ${
    hvass.1 $e |- A e. ~H $.
    hvass.2 $e |- B e. ~H $.
    hvass.3 $e |- C e. ~H $.
    $( Hilbert vector space associative law. $)
    hvassi $p |- ( ( A +h B ) +h C ) = ( A +h ( B +h C ) ) $=
      ( chil wcel cva co wceq ax-hvass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABCLM
      $.
      $( [3-Sep-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvadd23i $p |- ( ( A +h B ) +h C ) = ( ( A +h C ) +h B ) $=
      ( chil wcel cva co wceq hvadd23 mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM
      $.
      $( [18-Aug-1999] $)

    $( Hilbert vector space associative law for subtraction. $)
    hvsubassi $p |- ( ( A -h B ) -h C ) = ( A -h ( B +h C ) ) $=
      ( cmv co c1 cneg csm cva hvsubcli hvsubvali ax1cn negcli hvmulcli hvassi
      hvdistr1i opreq2i eqtr4i opreq1i hvaddcli 3eqtr4i eqtri ) ABGHZCGHUFIJZCK
      HZLHZABCLHZGHZUFCABDEMFNAUGBKHZLHZUHLHZAUGUJKHZLHZUIUKUNAULUHLHZLHUPAULUH
      DUGBIOPZEQUGCURFQRUOUQALUGBCUREFSTUAUFUMUHLABDENUBAUJDBCEFUCNUDUE $.
      $( [7-Oct-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvsub23i $p |- ( ( A -h B ) -h C ) = ( ( A -h C ) -h B ) $=
      ( cva co cmv hvcomi opreq2i hvsubassi 3eqtr4i ) ABCGHZIHACBGHZIHABIHCIHAC
      IHBIHNOAIBCEFJKABCDEFLACBDFELM $.
      $( [7-Oct-1999] $)

    $( Hilbert vector space commutative/associative law. $)
    hvadd12i $p |- ( A +h ( B +h C ) ) = ( B +h ( A +h C ) ) $=
      ( cva co hvcomi opreq1i hvassi 3eqtr3i ) ABGHZCGHBAGHZCGHABCGHGHBACGHGHMN
      CGABDEIJABCDEFKBACEDFKL $.
      $( [11-Sep-1999] $)

    ${
      hvadd4.4 $e |- D e. ~H $.
      $( Hilbert vector space addition law. $)
      hvadd4i $p |- ( ( A +h B ) +h ( C +h D ) ) =
                   ( ( A +h C ) +h ( B +h D ) ) $=
        ( chil wcel wa cva co wceq pm3.2i hvadd4 mp2an ) AIJZBIJZKCIJZDIJZKABLM
        CDLMLMACLMBDLMLMNRSEFOTUAGHOABCDPQ $.
        $( [3-Sep-1999] $)

      $( Hilbert vector space addition law. $)
      hvsubsub4i $p |- ( ( A -h B ) -h ( C -h D ) ) =
                      ( ( A -h C ) -h ( B -h D ) ) $=
        ( c1 cneg csm co cva cmv ax1cn negcli hvmulcli hvadd4i hvdistr1i
        opreq2i 3eqtr4i hvaddcli hvsubvali opreq12i ) AIJZBKLZMLZCUEDKLZMLZNLZA
        UECKLZMLZBUHMLZNLZABNLZCDNLZNLACNLZBDNLZNLUGUEUIKLZMLZULUEUMKLZMLZUJUNU
        GUKUEUHKLZMLZMLULUFVCMLZMLUTVBAUFUKVCEUEBIOPZFQZUECVFGQZUEUHVFUEDVFHQZQ
        RUSVDUGMUECUHVFGVISTVAVEULMUEBUHVFFVISTUAUGUIAUFEVGUBCUHGVIUBUCULUMAUKE
        VHUBBUHFVIUBUCUAUOUGUPUINABEFUCCDGHUCUDUQULURUMNACEGUCBDFHUCUDUA $.
        $( [31-Oct-1999] $)
    $}
  $}

  $( Hilbert vector space addition/subtraction law. $)
  hvsubsub4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) )
            -> ( ( A -h B ) -h ( C -h D ) ) = ( ( A -h C ) -h ( B -h D ) ) ) $=
    ( chil wcel cmv co wceq c0v cif opreq1 opreq1d eqeq12d opreq2 opreq2d
    ax-hv0cl elimel hvsubsub4i dedth4h ) AEFZBEFZCEFZDEFZABGHZCDGHZGHZACGHZBDGH
    ZGHZIUAAJKZBGHZUFGHZUKCGHZUIGHZIUKUBBJKZGHZUFGHZUNUPDGHZGHZIUQUCCJKZDGHZGHZ
    UKVAGHZUSGHZIUQVAUDDJKZGHZGHZVDUPVFGHZGHZIABCDJJJJAUKIZUGUMUJUOVKUEULUFGAUK
    BGLMVKUHUNUIGAUKCGLMNBUPIZUMURUOUTVLULUQUFGBUPUKGOMVLUIUSUNGBUPDGLPNCVAIZUR
    VCUTVEVMUFVBUQGCVADGLPVMUNVDUSGCVAUKGOMNDVFIZVCVHVEVJVNVBVGUQGDVFVAGOPVNUSV
    IVDGDVFUPGOPNUKUPVAVFAJEQRBJEQRCJEQRDJEQRST $.
    $( [2-Apr-2000] $)

  $( Two times a vector. $)
  hv2times $p |- ( A e. ~H -> ( 2 .h A ) = ( A +h A ) ) $=
    ( chil wcel c2 csm co c1 cva caddc cc wceq ax1cn ax-hvdistr2 mp3an12 df-2
    opreq1i syl5eq ax-hvdistr1 mp3an1 anidms hvaddcl ax-hvmulid syl 3eqtr2d )
    ABCZDAEFZGAEFZUGHFZGAAHFZEFZUIUEGGIFZAEFZUHUFGJCZUMUEULUHKLLGGAMNDUKAEOPQUE
    UJUHKZUMUEUEUNLGAARSTUEUIBCZUJUIKUEUOAAUATUIUBUCUD $.
    $( [22-Jun-2006] $) $( [22-Jun-2006] $)

  ${
    hvnegdi.1 $e |- A e. ~H $.
    hvnegdi.2 $e |- B e. ~H $.
    $( Distribution of negative over subtraction. $)
    hvnegdii $p |- ( -u 1 .h ( A -h B ) ) = ( B -h A ) $=
      ( c1 cneg cmv co csm cva hvsubvali opreq2i ax1cn negcli hvmulcli
      hvdistr1i cmul mul2negi mulid1i eqtri opreq1i hvmulassi chil wcel wceq
      ax-hvmulid ax-mp 3eqtr3i hvcomi 3eqtr4i 3eqtri ) EFZABGHZIHULAULBIHZJHZIH
      ULAIHZULUNIHZJHZBAGHZUMUOULIABCDKLULAUNEMNZCULBUTDOZPUQUPJHBUPJHURUSUQBUP
      JULULQHZBIHEBIHZUQBVBEBIVBEEQHEEEMMREMSTUAULULBUTUTDUBBUCUDVCBUEDBUFUGUHU
      AUPUQULAUTCOULUNUTVAOUIBADCKUJUK $.
      $( [31-Oct-1999] $)

    $( If the difference between two vectors is zero, they are equal. $)
    hvsubeq0i $p |- ( ( A -h B ) = 0h <-> A = B ) $=
      ( cmv co c0v wceq c1 cneg csm cva hvsubvali eqeq1i opreq1 sylbi ax1cn
      negcli hvmulcli hvadd23i hvassi hvnegidi opreq2i chil wcel ax-hvaddid
      ax-mp eqtri 3eqtri hvaddid2i 3eqtr3g hvsubid syl6eq impbii ) ABEFZGHZABHZ
      UPAIJZBKFZLFZBLFZGBLFZABUPUTGHVAVBHUOUTGABCDMNUTGBLOPVAABLFUSLFABUSLFZLFZ
      AAUSBCURBIQRDSZDTABUSCDVEUAVDAGLFZAVCGALBDUBUCAUDUEVFAHCAUFUGUHUIBDUJUKUQ
      UOBBEFZGABBEOBUDUEVGGHDBULUGUMUN $.
      $( [18-Aug-1999] $)

    $( Vector cancellation law. $)
    hvsubcan2i $p |- ( ( A +h B ) +h ( A -h B ) ) = ( 2 .h A ) $=
      ( cva co cmv c1 cneg csm c2 c0v hvsubvali opreq2i ax1cn negcli hvmulcli
      hvadd4i chil wcel wceq hv2times ax-mp eqcomi hvnegidi opreq12i eqtri 2cn
      ax-hvaddid 3eqtri ) ABEFZABGFZEFUKAHIZBJFZEFZEFZKAJFZLEFZUQULUOUKEABCDMNU
      PAAEFZBUNEFZEFURABAUNCDCUMBHOPDQRUSUQUTLEUQUSASTUQUSUACAUBUCUDBDUEUFUGUQS
      TURUQUAKAUHCQUQUIUCUJ $.
      $( [3-Sep-1999] $)

    ${
      hvaddcan.3 $e |- C e. ~H $.
      $( Cancellation law for vector addition. $)
      hvaddcani $p |- ( ( A +h B ) = ( A +h C ) <-> B = C ) $=
        ( cva co wceq c1 cneg csm opreq1 c0v ax1cn negcli hvmulcli hvadd23i
        hvnegidi opreq1i hvaddid2i 3eqtri 3eqtr3g opreq2 impbii ) ABGHZACGHZIZB
        CIUHUFJKZALHZGHZUGUJGHZBCUFUGUJGMUKAUJGHZBGHNBGHBABUJDEUIAJOPDQZRUMNBGA
        DSZTBEUAUBULUMCGHNCGHCACUJDFUNRUMNCGUOTCFUAUBUCBCAGUDUE $.
        $( [11-Sep-1999] $)

      $( Relationship between vector subtraction and addition. $)
      hvsubaddi $p |- ( ( A -h B ) = C <-> ( B +h C ) = A ) $=
        ( cmv co wceq c1 cneg csm cva hvsubvali eqeq1i c0v ax1cn negcli
        hvmulcli hvadd12i hvnegidi opreq2i chil wcel ax-hvaddid ax-mp 3eqtri
        hvaddcli hvaddcani eqcom 3bitr3i bitri ) ABGHZCIAJKZBLHZMHZCIZBCMHZAIZU
        MUPCABDENOBUPMHZURIAURIUQUSUTAURUTABUOMHZMHAPMHZABAUOEDUNBJQRESZTVAPAMB
        EUAUBAUCUDVBAIDAUEUFUGOBUPCEAUODVCUHFUIAURUJUKUL $.
        $( [11-Sep-1999] $)
    $}
  $}

  $( Distribution of negative over subtraction. $)
  hvnegdi $p |- ( ( A e. ~H /\ B e. ~H ) ->
                ( -u 1 .h ( A -h B ) ) = ( B -h A ) ) $=
    ( chil wcel c1 cneg cmv co csm wceq c0v cif opreq1 opreq2d opreq2 eqeq12d
    ax-hv0cl elimel hvnegdii dedth2h ) ACDZBCDZEFZABGHZIHZBAGHZJUCUAAKLZBGHZIHZ
    BUGGHZJUCUGUBBKLZGHZIHZUKUGGHZJABKKAUGJZUEUIUFUJUOUDUHUCIAUGBGMNAUGBGOPBUKJ
    ZUIUMUJUNUPUHULUCIBUKUGGONBUKUGGMPUGUKAKCQRBKCQRST $.
    $( [2-Apr-2000] $)

  $( If the difference between two vectors is zero, they are equal. $)
  hvsubeq0 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( A -h B ) = 0h <-> A = B ) ) $=
    ( chil wcel cmv co c0v wceq wb cif opreq1 eqeq1d eqeq1 bibi12d opreq2
    eqeq2 ax-hv0cl elimel hvsubeq0i dedth2h ) ACDZBCDZABEFZGHZABHZIUAAGJZBEFZGH
    ZUFBHZIUFUBBGJZEFZGHZUFUJHZIABGGAUFHZUDUHUEUIUNUCUGGAUFBEKLAUFBMNBUJHZUHULU
    IUMUOUGUKGBUJUFEOLBUJUFPNUFUJAGCQRBGCQRST $.
    $( [23-Oct-1999] $)

  $( If the sum of two vectors is zero, one is the negative of the other. $)
  hvaddeq0 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( ( A +h B ) = 0h <-> A = ( -u 1 .h B ) ) ) $=
    ( chil wcel wa cva co c0v wceq c1 cneg csm cmv hvaddsubval eqeq1d wb
    hvsubeq0 cc ax1cn negcli hvmulcl mpan sylan2 bitrd ) ACDZBCDZEZABFGZHIAJKZB
    LGZMGZHIZAUJIZUGUHUKHABNOUEUJCDZULUMPUFAUJQUIRDUFUNJSTUIBUAUBUCUD $.
    $( [19-Jun-2006] $) $( [10-Jun-2006] $)

  $( Cancellation law for vector addition. $)
  hvaddcan $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A +h B ) = ( A +h C ) <-> B = C ) ) $=
    ( chil wcel cva co wceq wb c0v cif opreq1 eqeq12d bibi1d opreq2 eqeq1d
    eqeq1 bibi12d eqeq2d eqeq2 ax-hv0cl elimel hvaddcani dedth3h ) ADEZBDEZCDEZ
    ABFGZACFGZHZBCHZIUEAJKZBFGZULCFGZHZUKIULUFBJKZFGZUNHZUPCHZIUQULUGCJKZFGZHZU
    PUTHZIABCJJJAULHZUJUOUKVDUHUMUIUNAULBFLAULCFLMNBUPHZUOURUKUSVEUMUQUNBUPULFO
    PBUPCQRCUTHZURVBUSVCVFUNVAUQCUTULFOSCUTUPTRULUPUTAJDUAUBBJDUAUBCJDUAUBUCUD
    $.
    $( [27-May-2005] $) $( [18-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvaddcan2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A +h C ) = ( B +h C ) <-> A = B ) ) $=
    ( chil wcel cva co wceq wb w3a ax-hvcom 3adant3 3adant2 eqeq12d hvaddcan
    bitr3d 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIRSTJZCAFGZCBFGZHUCUDUEUFUAUGUBR
    SUFUAHTCAKLRTUGUBHSCBKMNCABOPQ $.
    $( [2-Jun-2005] $) $( [18-May-2005] $)

  $( Cancellation law for scalar multiplication. $)
  hvmulcan $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ B e. ~H /\ C e. ~H )
               -> ( ( A .h B ) = ( A .h C ) <-> B = C ) ) $=
    ( cc wcel cc0 wne wa chil w3a cmv co c0v wceq wo csm wb wn df-ne biorf
    sylbi ad2antlr 3adant3 hvsubeq0 3adant1 hvsubdistr1 eqeq1d hvmul0or
    hvsubcl sylan2 3impb hvmulcl 3adant2 sylanc 3bitr3d 3adant1r 3bitr3rd ) ADE
    ZAFGZHZBIEZCIEZJBCKLZMNZAFNZVDOZBCNZABPLZACPLZNZUTVAVDVFQZVBUSVKURVAUSVERVK
    AFSVEVDTUAUBUCVAVBVDVGQUTBCUDUEURVAVBVFVJQUSURVAVBJZAVCPLZMNZVHVIKLZMNZVFVJ
    VLVMVOMABCUFUGURVAVBVNVFQZURVCIEVQVAVBHAVCUHBCUIUJUKVHIEZVIIEZVPVJQVLVHVIUD
    URVAVRVBABULUCURVBVSVAACULUMUNUOUPUQ $.
    $( [30-Dec-2008] $) $( [19-May-2005] $)

  $( Cancellation law for scalar multiplication. $)
  hvmulcanOLD $p |- ( ( ( A e. CC /\ B e. ~H /\ C e. ~H ) /\ A =/= 0 ) ->
                  ( ( A .h B ) = ( A .h C ) <-> B = C ) ) $=
    ( cc wcel chil w3a cc0 wne wa cmv co c0v wceq csm wo wb wn df-ne biorf
    sylbi adantl hvmul0or hvsubcl sylan2 3impb adantr bitr4d hvsubdistr1
    eqeq1d hvsubeq0 hvmulcl 3adant3 3adant2 sylanc 3bitrd 3adant1 bitr3d ) ADEZ
    BFEZCFEZGZAHIZJZBCKLZMNZABOLZACOLZNZBCNZVDVFAVEOLZMNZVGVHKLZMNZVIVDVFAHNZVF
    PZVLVCVFVPQZVBVCVORVQAHSVOVFTUAUBVBVLVPQZVCUSUTVAVRUSVEFEVRUTVAJAVEUCBCUDUE
    UFUGUHVDVKVMMVBVKVMNVCABCUIUGUJVBVNVIQZVCVGFEZVHFEZVSVBVGVHUKUSUTVTVAABULUM
    USVAWAUTACULUNUOUGUPVBVFVJQZVCUTVAWBUSBCUKUQUGUR $.
    $( [22-May-2005] $) $( [19-May-2005] $)

  $( Cancellation law for scalar multiplication. $)
  hvmulcan2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. ~H /\ C =/= 0h ) ) ->
                  ( ( A .h C ) = ( B .h C ) <-> A = B ) ) $=
    ( cc wcel chil c0v wne wa w3a csm co cmv wceq wb hvsubeq0 hvmulcl 3adant2
    3adant1 sylanc 3adant3r cmin cc0 wo hvsubdistr2 eqeq1d hvmul0or subcl
    sylan 3impa bitr3d wn df-ne biorf orcom syl6bb sylbi ad2antll subeq0
    3adant3 3bitr2d ) ADEZBDEZCFEZCGHZIZJZACKLZBCKLZMLZGNZVHVINZABNZVBVCVDVKVLO
    ZVEVHFEZVIFEZVNVBVCVDJZVHVIPVBVDVOVCACQRVCVDVPVBBCQSTUAVGVKABUBLZUCNZCGNZUD
    ZVSVMVBVCVDVKWAOVEVQVRCKLZGNZVKWAVQWBVJGABCUEUFVBVCVDWCWAOZVRDEVDWDVBVCIVRC
    UGABUHUIUJUKUAVCVFVSWAOZVBVEWEVCVDVEVTULZWECGUMWFVSVTVSUDWAVTVSUNVTVSUOUPUQ
    URSVBVCVSVMOVFABUSUTVAUK $.
    $( [2-Jan-2009] $) $( [19-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvsubcan $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h B ) = ( A -h C ) <-> B = C ) ) $=
    ( chil wcel w3a cmv co wceq c1 cneg csm cva hvsubval 3adant3 3adant2
    eqeq12d wb hvaddcan cc ax1cn negcli hvmulcl mpan syl3an3 syl3an2 cc0 wne
    ax1ne0 negne0i hvmulcanOLD mpan2 mp3an1 3adant1 3bitrd ) ADEZBDEZCDEZFZABGH
    ZACGHZIAJKZBLHZMHZAVBCLHZMHZIZVCVEIZBCIZUSUTVDVAVFUPUQUTVDIURABNOUPURVAVFIU
    QACNPQUPVCDEZURVGVHRZUQUPVJVEDEZVKURAVCVESVBTEZURVLJUAUBZVBCUCUDUEVMUQVJVNV
    BBUCUDUFUQURVHVIRZUPVMUQURVOVNVMUQURFVBUGUHVOJUAUIUJVBBCUKULUMUNUO $.
    $( [5-Jun-2005] $) $( [18-May-2005] $)

  $( Cancellation law for vector addition. $)
  hvsubcan2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h C ) = ( B -h C ) <-> A = B ) ) $=
    ( chil wcel cmv co wceq wb w3a c1 cneg csm cc ax1cn negcli cc0 wne ax1ne0
    negne0i hvmulcanOLD mpan2 mp3an1 hvsubcl 3adant3 3adant2 sylanc hvnegdi
    eqeq12d hvsubcan 3bitr3d 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIUMUNUOJZKLZCA
    FGZMGZVACBFGZMGZHZVBVDHZURUSVBDEZVDDEZVFVGIZUTVANEZVHVIVJKOPVKVHVIJVAQRVJKO
    STVAVBVDUAUBUCUMUNVHUOCAUDUEUMUOVIUNCBUDUFUGUTVCUPVEUQUMUNVCUPHUOCAUHUEUMUO
    VEUQHUNCBUHUFUICABUJUKUL $.
    $( [26-Jun-2005] $) $( [18-May-2005] $)

  $( Subtraction of a zero vector. $)
  hvsub0 $p |- ( A e. ~H -> ( A -h 0h ) = A ) $=
    ( chil wcel c0v cmv co cva c1 cneg csm wceq ax-hv0cl hvsubval mpan2 cc
    ax1cn negcli hvmul0 ax-mp opreq2i syl6eq ax-hvaddid eqtrd ) ABCZADEFZADGFZA
    UDUEAHIZDJFZGFZUFUDDBCUEUIKLADMNUHDAGUGOCUHDKHPQUGRSTUAAUBUC $.
    $( [2-Apr-2000] $)

  $( Relationship between vector subtraction and addition. $)
  hvsubadd $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                  ( ( A -h B ) = C <-> ( B +h C ) = A ) ) $=
    ( chil wcel cmv co wceq cva wb c0v cif opreq1 eqeq1d eqeq2 bibi12d opreq2
    ax-hv0cl elimel hvsubaddi dedth3h ) ADEZBDEZCDEZABFGZCHZBCIGZAHZJUBAKLZBFGZ
    CHZUGUIHZJUIUCBKLZFGZCHZUMCIGZUIHZJUNUDCKLZHZUMURIGZUIHZJABCKKKAUIHZUFUKUHU
    LVBUEUJCAUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFQNVCUGUPUIBUMCIMNPCURHZUOUS
    UQVACURUNOVDUPUTUICURUMIQNPUIUMURAKDRSBKDRSCKDRSTUA $.
    $( [30-Oct-1999] $)

  $( Hilbert vector space addition/subtraction law. $)
  hvaddsub4 $p |- ( ( ( A e. ~H /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
          ( ( A +h B ) = ( C +h D ) <-> ( A -h C ) = ( D -h B ) ) ) $=
    ( chil wcel wa cva co cmv wceq wb hvsubcan2 hvaddcl adantr adantl ancoms
    ad2ant2lr syl3anc c0v pm3.27 anim2i hvsub4 syldan hvsubid ad2antlr opreq2d
    hvsubcl ax-hvaddid syl adantlr 3eqtrd adantrr pm3.26 anim1i ad2antrr
    opreq1d hvaddid2 adantll eqeq12d bitr3d ) AEFZBEFZGZCEFZDEFZGZGZABHIZCBHIZJ
    IZCDHIZVJJIZKZVIVLKZACJIZDBJIZKVIEFZVLEFZVJEFZVNVOLVHVIVLVJMVDVRVGABNOVGVSV
    DCDNPVCVEVTVBVFVEVCVTCBNQRSVHVKVPVMVQVDVEVKVPKVFVDVEGZVKVPBBJIZHIZVPTHIZVPV
    DVEVEVCGZVKWCKVEVDWEVDVCVEVBVCUAUBQABCBUCUDWAWBTVPHVCWBTKVBVEBUEUFUGVBVEWDV
    PKZVCVBVEGVPEFWFACUHVPUIUJUKULUMVCVGVMVQKZVBVGVCWGVGVCGZVMCCJIZVQHIZTVQHIZV
    QVGVCWEVMWJKVGVEVCVEVFUNUOCDCBUCUDWHWITVQHVEWITKVFVCCUEUPUQVFVCWKVQKZVEVFVC
    GVQEFWLDBUHVQURUJUSULQUSUTVA $.
    $( [30-Jun-2005] $) $( [18-May-2005] $)

