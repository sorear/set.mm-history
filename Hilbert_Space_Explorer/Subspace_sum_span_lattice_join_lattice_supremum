$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Subspace sum, span, lattice join, lattice supremum
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u $.
    $( Define subspace sum in ` SH ` .  See ~ shsumvalt , ~ shsumval2 , and
       ~ shsumval3 for its value. $)
    df-shsum $a |- +H = { <. <. x , y >. , z >. | ( ( x e. SH /\ y e. SH ) /\
                 z = { w e. H~ | E. v e. x E. u e. y w = ( v +h u ) } ) } $.

    $( Define the linear span of a subset of Hilbert space.  Definition
       of span in [Schechter] p. 276.  See ~ spanvalt for its value. $)
    df-span $a |- span = { <. x , y >. | ( x (_ H~ /\
                   y = |^| { z e. SH | x (_ z } ) } $.

    $( Define Hilbert lattice join.  See ~ chjvalt for its value and
       ~ chjclt for its closure law.  Note that we define it over all Hilbert
       space subsets to allow proving more general theorems.  Even for general
       subsets the join belongs to ` CH ` ; see ~ sshjclt .  For an alternate
       definition see ~ dfchj2 . $)
    df-chj $a |- vH = { <. <. x , y >. , z >. | ( ( x (_ H~ /\ y (_ H~ ) /\
               z = ( _|_ ` ( _|_ ` ( x u. y ) ) ) ) } $.

    $( Define the supremum of a set of Hilbert lattice elements.  See
       ~ chsupval2t for its value and ~ dfchsup2 for an alternate definition.
       We actually define the supremum for an arbitrary collection of Hilbert
       space subsets, not just elements of the Hilbert lattice ` CH ` , to
       allow more general theorems.  Even for general subsets the supremum
       still a Hilbert lattice element; see ~ hsupclt . $)
    df-chsup $a |- \/H = { <. x , y >. | ( x (_ P~ H~ /\
                   y = ( _|_ ` ( _|_ ` U. x ) ) ) } $.

  $}

  ${
    $d x y z w f g v u A $.  $d x y z w f g v u B $.  $d x y z C $.
    $d x y D $.
    $( Value of subspace sum of two Hilbert space subspaces.  Definition of
       subspace sum in [Kalmbach] p. 65. $)
    shsumvalt $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) =
        { x e. H~ | E. y e. A E. z e. B x = ( y +h z ) } ) $=
      ( vw vv vu csh cv cva co wceq wrex chil crab cph ax-hilex rabex rexeq1
      rabbisdv rexbidv df-shsum oprabval2 ) FGHDEIIAJBJCJKLMZCGJZNZBFJZNZAOPUEC
      ENZBDNZAOPQUGBDNZAOPUKAORSUHDMUIULAOUGBUHDTUAUFEMZULUKAOUMUGUJBDUECUFETUB
      UAFGHABCUCUD $.
      $( [16-Oct-1999] $)

    $( Membership in the subspace sum of two Hilbert subspaces. $)
    shselt $p |- ( ( A e. SH /\ B e. SH ) -> ( C e. ( A +H B ) <->
                 E. x e. A E. y e. B C = ( x +h y ) ) ) $=
      ( vz csh wcel wa cph co chil cv cva wceq wrex crab shsumvalt eleq2d
      eqeq1 2rexbidv elrab syl6bb wi shss sseld im2anan9 hvaddclt eleq1a syl
      syl6 r19.23advv pm4.71rd bitr4d ) CGHZDGHZIZECDJKZHZELHZEAMZBMZNKZOZBDPAC
      PZIZVEUQUSEFMZVCOZBDPACPZFLQZHVFUQURVJEFABCDRSVIVEFELVGEOVHVDABCDVGEVCTUA
      UBUCUQVEUTUQVDUTABCDUQVACHZVBDHZIVALHZVBLHZIZVDUTUDZUOVKVMUPVLVNUOCLVACUE
      UFUPDLVBDUEUFUGVOVCLHVPVAVBUHVCLEUIUJUKULUMUN $.
      $( [16-Dec-2004] $) $( [14-Dec-2004] $)

    $( Membership in the subspace sum of two Hilbert subspaces, using vector
       subtraction. $)
    shsel3t $p |- ( ( A e. SH /\ B e. SH ) -> ( C e. ( A +H B ) <->
                 E. x e. A E. y e. B C = ( x -h y ) ) ) $=
      ( vz csh wcel wa cph co cv cva wceq wrex cmv shselt c1 cneg csm id chil
      hvaddsubvalt shelt syl2an an4s anassrs sylan9eqr opreq2 eqeq2d rcla4ev
      cc ax1cn negcl shmulclt mp3an2 adantll adantlr sylan syldan ex
      r19.23adva hvsubvalt impbid rexbidva bitrd ) CGHZDGHZIZECDJKHEALZFLZMKZNZ
      FDOZACOEVJBLZPKZNZBDOZACOAFCDEQVIVNVRACVIVJCHZIZVNVRVTVMVRFDVTVKDHZIZVMVR
      WBVMEVJRSZVKTKZPKZNZVRVMWBEVLWEVMUAVIVSWAVLWENZVGVSVHWAWGVJUBHZVKUBHWGVGV
      SIZVHWAIVJVKUCVJCUDZVKDUDUEUFUGUHWDDHZWFVRWBVQWFBWDDVOWDNVPWEEVOWDVJPUIUJ
      UKVIWAWKVSVHWAWKVGVHWCULHZWAWKRUMUNZWCVKDUOUPUQURUSUTVAVBVTVQVNBDVTVODHZI
      ZVQVNWOVQEVJWCVOTKZMKZNZVNVQWOEVPWQVQUAVIVSWNVPWQNZVGVSVHWNWSWHVOUBHWSWIV
      HWNIVJVOVCWJVODUDUEUFUGUHWPDHZWRVNWOVMWRFWPDVKWPNVLWQEVKWPVJMUIUJUKVIWNWT
      VSVHWNWTVGVHWLWNWTWMWCVODUOUPUQURUSUTVAVBVDVEVF $.
      $( [23-Feb-2007] $) $( [20-Jan-2007] $)

    ${
      shscl.1 $e |- A e. SH $.
      shscl.2 $e |- B e. SH $.
      $( Membership in subspace sum. $)
      shsel $p |- ( C e. ( A +H B ) <-> E. x e. A E. y e. B C = ( x +h y ) ) $=
        ( csh wcel cph co cv cva wceq wrex wb shselt mp2an ) CHIDHIECDJKIEALBLM
        KNBDOACOPFGABCDEQR $.
        $( [4-May-2000] $)

      $( Closure of subspace sum. $)
      shscl $p |- ( A +H B ) e. SH $=
        ( vx vy vz vw vv vu vf vg cph co csh wcel chil wss c0v wa cv cva wral
        csm cc sh wceq wrex crab shsumvalt mp2an ssrab2 eqsstr sh0 ax-mp
        ax-hv0cl hvaddid2 eqcomi rcla4eopr mp3an shsel mpbir pm3.2i wi
        shaddcltOLD ad2ant2r ad2ant2l opreq12 hvadd4t shel anim12i syl2an an4s
        eqtr4d syl3anc ancoms exp43 r19.23aivv com3l imp anbi12i 3imtr4 rgen2a
        shmulclt mp3an1 adantrr adantrl opreq2 adantl ad2antll ax-hvdistr1 id
        syl3an 3expb adantrrr eqtrd exp42 impcom anbi2i rgen2 mpbir2an ) ABMNZO
        PXBQRZSXBPZTEUAZFUAZUBNZXBPZFXBUCEXBUCZXEXFUDNZXBPZFXBUCEUEUCZTEFXBUFXC
        XDXBGUAZXGUGFBUHEAUHZGQUIZQAOPZBOPZXBXOUGCDGEFABUJUKXNGQULUMXDSXGUGFBUH
        EAUHZSAPZSBPZSSSUBNZUGXRXPXSCAUNUOXQXTDBUNUOYASSUPUQUREFABSSSUBUSUTEFAB
        SCDVAVBVCXIXLXHEFXBXEXMHUAZUBNZUGZHBUHGAUHZXFIUAZJUAZUBNZUGZJBUHIAUHZTX
        GKUALUAUBNZUGLBUHKAUHZXEXBPZXFXBPZTXHYEYJYLYDYJYLVDGHABYJXMAPZYBBPZTZYD
        YLYIYQYDYLVDVDIJABYFAPZYGBPZTZYIYQYDYLYQYDTZYTYITZYLXMYFUBNZAPZYBYGUBNZ
        BPZXGUUCUUEUBNZUGYLUUAUUBTZKLABUUCUUEXGUBUSYQYTUUDYDYIYOYRUUDYPYSXPYOYR
        TZUUDVDCXMYFAVEUOVFVFYQYTUUFYDYIYPYSUUFYOYRXQYPYSTZUUFVDDYBYGBVEUOVGVFU
        UHXGYCYHUBNZUUGYDYIXGUUKUGYQYTXEYCXFYHUBVHVGYQYTUUGUUKUGZYDYIYOYRYPYSUU
        LXMQPZYFQPZTYBQPZYGQPZTUULUUIUUJXMYFYBYGVIYOUUMYRUUNXMACVJYFACVJZVKYPUU
        OYSUUPYBBDVJYGBDVJZVKVLVMVFVNVOVPVQVRVSVRVTYMYEYNYJGHABXECDVAIJABXFCDVA
        ZWAKLABXGCDVAWBWCXKEFUEXBXEUEPZYJTXJYKUGLBUHKAUHZUUTYNTXKYJUUTUVAYIUUTU
        VAVDZIJABYRYSYIUVBVDYRYSYIUUTUVAUUTYRYSYITZTZUVAXEYFUDNZAPZXEYGUDNZBPZX
        JUVEUVGUBNZUGUVAUUTUVDTZKLABUVEUVGXJUBUSUUTYRUVFUVCXPUUTYRUVFCXEYFAWDWE
        WFUUTUVCUVHYRUUTYSUVHYIXQUUTYSUVHDXEYGBWDWEWFWGUVJXJXEYHUDNZUVIUVCXJUVK
        UGZUUTYRYIUVLYSXFYHXEUDWHWIWJUUTYRYSUVKUVIUGZYIUUTYRYSUVMUUTUUNUUPUVMUU
        TYRYSXEYFYGWKUUTWLUUQUURWMWNWOWPVOVPWQVTVRWRYNYJUUTUUSWSKLABXJCDVAWBWTV
        CXA $.
        $( [15-Oct-1999] $)
    $}

    $( Closure of subspace sum. $)
    shsclt $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) e. SH ) $=
      ( csh wcel cph co chil cif wceq opreq1 eleq1d opreq2 helsh elimel shscl
      dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCC
      BUBTELKTUBAGCMNBGCMNOP $.
      $( [3-Jan-2005] $) $( [15-Dec-2004] $)

    $( Commutative law for subspace sum. $)
    shscomt $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) = ( B +H A ) ) $=
      ( vx vy vz csh wcel wa cph co cv cva wceq wrex chil shelt anim12i an4s
      ax-hvcom syl eqeq2d 2rexbidva rexcom syl6bb shselt wb ancoms 3bitr4d
      eqrdv ) AFGZBFGZHZCABIJZBAIJZULCKZDKZEKZLJZMZEBNDANZUOUQUPLJZMZDANEBNZUOU
      MGUOUNGZULUTVBEBNDANVCULUSVBDEABULUPAGZUQBGZHHZURVAUOVGUPOGZUQOGZHZURVAMU
      JVEUKVFVJUJVEHVHUKVFHVIUPAPUQBPQRUPUQSTUAUBVBDEABUCUDDEABUOUEUKUJVDVCUFED
      BAUOUEUGUHUI $.
      $( [17-Dec-2004] $) $( [15-Dec-2004] $)

    $( Vector sum belongs to subspace sum. $)
    shsvat $p |- ( ( A e. SH /\ B e. SH ) -> ( ( C e. A /\ D e. B ) ->
                 ( C +h D ) e. ( A +H B ) ) ) $=
      ( vx vy csh wcel wa cva co cph cv wceq wrex shselt eqid rcla4eopr mp3an3
      syl5bir ) AGHBGHICDJKZABLKHUAEMFMJKNFBOEAOZCAHZDBHZIEFABUAPUCUDUAUANUBUAQ
      EFABCDUAJRST $.
      $( [17-Dec-2004] $) $( [15-Dec-2004] $)

    $( A subspace sum contains a member of one of its subspaces. $)
    shsel1t $p |- ( ( A e. SH /\ B e. SH ) ->
                  ( C e. A -> C e. ( A +H B ) ) ) $=
      ( csh wcel wa cph co c0v cva wceq chil shelt ax-hvaddid syl adantlr sh0
      adantl shsvat mpan2d imp eqeltrrd ex ) ADEZBDEZFZCAEZCABGHZEUFUGFCIJHZCUH
      UDUGUICKZUEUDUGFCLEUJCAMCNOPUFUGUIUHEZUFUGIBEZUKUEULUDBQRABCISTUAUBUC $.
      $( [17-Dec-2004] $) $( [15-Dec-2004] $)

    $( A subspace sum contains a member of one of its subspaces. $)
    shsel2t $p |- ( ( A e. SH /\ B e. SH ) ->
                 ( C e. B -> C e. ( A +H B ) ) ) $=
      ( csh wcel wa cph co wi shsel1t ancoms shscomt eleq2d sylibrd ) ADEZBDEZF
      ZCBEZCBAGHZEZCABGHZEPORTIBACJKQUASCABLMN $.
      $( [17-Dec-2004] $) $( [15-Dec-2004] $)

    $( Vector subtraction belongs to subspace sum. $)
    shsvst $p |- ( ( A e. SH /\ B e. SH ) ->
                ( ( C e. A /\ D e. B ) -> ( C -h D ) e. ( A +H B ) ) ) $=
      ( csh wcel wa cph co cmv wi shsclt shsubcltOLD syl shsel1t shsel2t
      syl2and ) AEFBEFGZCABHIZFZDSFZCDJISFZCAFDBFRSEFTUAGUBKABLCDSMNABCOABDPQ
      $.
      $( [3-Jan-2005] $) $( [15-Dec-2004] $)

    $( Subspace sum is an upper bound of its arguments. $)
    shsub1t $p |- ( ( A e. SH /\ B e. SH ) -> A (_ ( A +H B ) ) $=
      ( vx csh wcel wa cph co cv shsel1t ssrdv ) ADEBDEFCAABGHABCIJK $.
      $( [16-Dec-2004] $) $( [14-Dec-2004] $)

    $( Subspace sum is an upper bound of its arguments. $)
    shsub2t $p |- ( ( A e. SH /\ B e. SH ) -> A (_ ( B +H A ) ) $=
      ( csh wcel wa cph co shsub1t shscomt sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
      $( [21-Dec-2004] $) $( [17-Dec-2004] $)

    $( The orthocomplement of the zero subspace is the unit subspace. $)
    choc0 $p |- ( _|_ ` 0H ) = H~ $=
      ( vx vy c0h cort cfv chil cv wcel csp co cc0 wceq wral wa wi abai csh wb
      h0elsh shocelt ax-mp c0v hi02t wal df-ral elch0 imbi1i albii ax-hv0cl
      elisseti opreq2 eqeq1d ceqsalv 3bitr sylibr biantru 3bitr4 eqriv ) ACDEZF
      AGZFHZUTBGZIJZKLZBCMZNZVAVAVEOZNUTUSHZVAVAVEPCQHVHVFRSBUTCTUAVGVAVAUTUBIJ
      ZKLZVEUTUCVEVBCHZVDOZBUDVBUBLZVDOZBUDVJVDBCUEVLVNBVKVMVDVBUFUGUHVDVJBUBUB
      FUIUJVMVCVIKVBUBUTIUKULUMUNUOUPUQUR $.
      $( [15-Oct-1999] $)

    $( The orthocomplement of the unit subspace is the zero subspace.  Does
       not require Axiom of Choice. $)
    choc1 $p |- ( _|_ ` H~ ) = 0H $=
      ( vx vy chil cort cfv c0h cv wcel c0v wceq csp co cc0 wral csh wa wb
      helsh shocelt ax-mp pm3.27bi wss shocss sseli hial0 syl mpbid elch0
      sylibr ssriv h0elsh shococss choc0 fveq2i sseqtr eqssi ) CDEZFAUQFAGZUQHZ
      URIJZURFHUSURBGKLMJBCNZUTUSURCHZVACOHZUSVBVAPQRBURCSTUAUSVBVAUTQUQCURVCUQ
      CUBRCUCTUDBURUEUFUGURUHUIUJFFDEZDEZUQFOHFVEUBUKFULTVDCDUMUNUOUP $.
      $( [24-Oct-1999] $)

    ${
      $d x y $.
      $( Orthogonal complement of the empty set. $)
      chocnul $p |- ( _|_ ` (/) ) = H~ $=
        ( vx vy c0 cort cfv chil cv wcel csp co cc0 wceq wral wss wa wb 0ss
        ocelt ax-mp ral0 mpbiran2 eqriv ) ACDEZFAGZUCHZUDFHZUDBGIJKLZBCMZCFNUEU
        FUHOPFQBUDCRSUGBTUAUB $.
        $( [31-Oct-2000] $)
    $}

    ${
      shintcl.1 $e |- ( A (_ SH /\ A =/= (/) ) $.
      $( Closure of intersection of a non-empty subset of ` SH ` . $)
      shintcl $p |- |^| A e. SH $=
        ( vx vy vz cint csh wcel chil wss c0v wa cv cva co wral csm cc sh c0
        wne pm3.27i wex ne0 intss1 pm3.26i sseli shss syl sstrd 19.23aiv sylbi
        ax-mp ax-hv0cl elisseti elint2 sh0 mprgbir pm3.2i shaddclt syl3an1
        3expib elinti com12 syl2and r19.21aiv oprex sylibr rgen2a shmulclt
        sylan2d rgen2 mpbir2an ) AFZGHVNIJZKVNHZLCMZDMZNOZVNHZDVNPCVNPZVQVRQOZV
        NHZDVNPCRPZLCDVNSVOVPATUAZVOAGJZWEBUBWEEMZAHZEUCVOEAUDWHVOEWHVNWGIWGAUE
        WHWGGHZWGIJAGWGWFWEBUFUGZWGUHUIUJUKULUMVPKWGHZEAEKAKIUNUOUPWHWIWKWJWGUQ
        UIURUSWAWDVTCDVNVQVNHZVRVNHZLZVSWGHZEAPVTWNWOEAWHWNWOWHVQWGHZVRWGHZWOWL
        WMWHWPWQWOWIWPWQWOWHVQVRWGUTWJVAVBWLWHWPVQAWGVCVDWMWHWQVRAWGVCVDZVEVDVF
        EVSAVQVRNVGUPVHVIWCCDRVNVQRHZWMLZWBWGHZEAPWCWTXAEAWHWTXAWHWSWQXAWMWHWSW
        QXAWIWSWQXAWHVQVRWGVJWJVAVBWRVKVDVFEWBAVQVRQVGUPVHVLUSVM $.
        $( [19-Jan-2007] $) $( [14-Oct-1999] $)
    $}

    $( The intersection of a non-empty set of subspaces is a subspace. $)
    shintclt $p |- ( ( A (_ SH /\ A =/= (/) ) -> |^| A e. SH ) $=
      ( csh wss c0 wne wa cint wcel cif wceq inteq eleq1d sseq1 neeq1 anbi12d
      ssid c0h h0elsh ne0i ax-mp pm3.2i elimhyp shintcl dedth ) ABCZADEZFZAGZBH
      UGABIZGZBHABAUIJZUHUJBAUIKLUIUGUIBCZUIDEZFBBCZBDEZFABUKUEULUFUMAUIBMAUIDN
      OBUIJUNULUOUMBUIBMBUIDNOUNUOBPQBHUORBQSTUAUBUCUD $.
      $( [20-May-2007] $) $( [2-Jun-2004] $)

    ${
      chintcl.1 $e |- ( A (_ CH /\ A =/= (/) ) $.
      $( The intersection of a non-empty set of closed subspaces is a
         closed subspace. $)
      chintcl $p |- |^| A e. CH $=
        ( vf vx vy cint cch wcel csh cn cv wf chli wbr wa wi wal closedsub wss
        c0 wne pm3.26i chsssh sstri pm3.27i pm3.2i shintcl wral sseli visset
        chlim syl exp3a com3r imp r19.20dva fint elint2 3imtr4g impcom gen2
        mpbir2an ) AFZGHVCIHJVCCKZLZVDDKZMNZOVFVCHZPZDQCQDCVCRAAISATUAZAGIAGSZV
        JBUBZUCUDVKVJBUEZUFUGVICDVGVEVHVGJEKZVDLZEAUHVFVNHZEAUHVEVHVGVOVPEAVGVN
        AHZVOVPPVQVOVGVPVQVOVGVPVQVNGHVOVGOVPPAGVNVLUIVFVDVNDUJZUKULUMUNUOUPEJA
        VDVMUQEVFAVRURUSUTVAVB $.
        $( [23-Jan-2007] $) $( [14-Oct-1999] $)
    $}

    $( The intersection (infimum) of a non-empty subset of ` CH ` belongs
       to ` CH ` .  Part of Theorem 3.13 of [Beran] p. 108.  Also part of
       Definition 3.4-1 in [MegillPavicic] p. 2345 (PDF p. 8). $)
    chintclt $p |- ( ( A (_ CH /\ A =/= (/) ) -> |^| A e. CH ) $=
      ( cch wss c0 wne wa cint wcel cif wceq inteq eleq1d sseq1 neeq1 anbi12d
      ssid c0h h0elch ne0i ax-mp pm3.2i elimhyp chintcl dedth ) ABCZADEZFZAGZBH
      UGABIZGZBHABAUIJZUHUJBAUIKLUIUGUIBCZUIDEZFBBCZBDEZFABUKUEULUFUMAUIBMAUIDN
      OBUIJUNULUOUMBUIBMBUIDNOUNUOBPQBHUORBQSTUAUBUCUD $.
      $( [26-Feb-2007] $) $( [14-Oct-1999] $)

    $( The double complement is the smallest closed subspace containing
       a subset of Hilbert space.  Remark 3.12(B) of [Beran] p. 107. $)
    ococint $p |- ( A (_ H~ ->
                  ( _|_ ` ( _|_ ` A ) ) = |^| { x e. CH | A (_ x } ) $=
      ( chil wss cort cfv cv cch crab cint wa occont wcel helch jctl sseq2
      elrab sylibr intss1 ocss 3syl jca ssintub wi syl mpdan mpi sylc c0 wne
      wceq wrex rcla4ev mpan rabn0 ssrab2 jctil chintclt ococt sseqtrd occlt
      ococss eqssd ) BCDZBEFZEFZBAGZDZAHIZJZVDVFVJEFZEFZVJVKCDZVECDZKVKVEDZVFVL
      DVDVKVELVDVMVNVDCVIMZVJCDZVMVDCHMZVDKVPVDVRNOVHVDACHVGCBPZQRZCVISZVJTUABT
      ZUBVDBVJDZVOABHUCVDVQWCVOUDVDVPVQVTWAUEBVJLUFUGUHVDVIHDZVIUIUJZKVJHMVLVJU
      KVDWEWDVDVHAHULZWEVRVDWFNVHVDACHVSUMUNVHAHUORVHAHUPUQVIURVJUSUAUTVDVFVIMZ
      VJVFDVDVFHMZBVFDZKWGVDWHWIVDVNWHWBVEVAUEBVBUBVHWIAVFHVGVFBPQRVFVISUEVC $.
      $( [8-Aug-2000] $)

    $( Alternate definition of supremum of a subset of the Hilbert lattice.
       Definition in Proposition 1 of [Kalmbach] p. 65.  We actually define it
       on any collection of Hilbert space subsets, not just the Hilbert lattice
       ` CH ` , to allow more general theorems. $)
    dfchsup2 $p |- \/H = { <. x , y >. | ( x (_ P~ H~ /\
                   y = |^| { z e. CH | U. x (_ z } ) } $=
      ( chsup cv chil cpw wss cuni cort cfv wceq wa copab cch crab cint
      df-chsup uniss unipw syl6ss ococint syl eqeq2d pm5.32i opabbii eqtr ) DAE
      ZFGZHZBEZUHIZJKJKZLZMZABNUJUKULCEHCOPQZLZMZABNABRUOURABUJUNUQUJUMUPUKUJUL
      FHUMUPLUJULUIIFUHUISFTUACULUBUCUDUEUFUG $.
      $( [9-Dec-2003] $) $( [9-Dec-2003] $)

    $( Value of the linear span of a subset of Hilbert space.  The span is the
       intersection of all subspaces constraining the subset.  Definition
       of span in [Schechter] p. 276. $)
    spanvalt $p |- ( A (_ H~ -> ( span ` A ) = |^| { x e. SH | A (_ x } ) $=
      ( vy vz chil wss cpw wcel cspn cfv cv csh crab cint wceq ax-hilex elpw2
      cvv wrex helsh sseq2 rcla4ev mpan sylbi intexrab sylib sseq1 rabbisdv
      inteqd wa copab df-span anbi1i opabbii eqtr4 fvopab4g mpdan sylbir ) BEFZ
      BEGZHZBIJBAKZFZALMZNZOZBEPQZVAVERHZVFVAVCALSZVHVAUSVIVGELHUSVITVCUSAELVBE
      BUAUBUCUDVCALUEUFCDBCKZVBFZALMZNZVEUTRIVJBOZVLVDVNVKVCALVJBVBUGUHUIIVJEFZ
      DKVMOZUJZCDUKVJUTHZVPUJZCDUKCDAULVSVQCDVRVOVPVJEPQUMUNUOUPUQUR $.
      $( [3-Jun-2004] $) $( [2-Jun-2004] $)

    $( Value of supremum of set of subsets of Hilbert space.  Definition of
       supremum in Proposition 1 of [Kalmbach] p. 65. $)
    hsupval2t $p |- ( A (_ P~ H~ ->
                    ( \/H ` A ) = |^| { x e. CH | U. A (_ x } ) $=
      ( vy vz chil cpw wcel cuni cv wss cch crab cint cvv chsup cfv wceq unieq
      sseq1d rabbisdv inteqd wa copab dfchsup2 ax-hilex pwex elpw2 anbi1i
      opabbii eqtr4 fvopab4g biimpr c0 wne uniss unipw syl6ss helch jctil
      sseq2 elrab sylibr ne0i syl intex sylib sylanc ) BEFZFZGZBHZAIZJZAKLZMZNG
      ZBOPVOQBVHJZCDBCIZHZVLJZAKLZMZVOVINOVRBQZWAVNWCVTVMAKWCVSVKVLVRBRSTUAOVRV
      HJZDIWBQZUBZCDUCVRVIGZWEUBZCDUCCDAUDWHWFCDWGWDWEVRVHEUEUFZUGUHUIUJUKVJVQB
      VHWIUGULVQVNUMUNZVPVQEVNGZWJVQEKGZVKEJZUBWKVQWMWLVQVKVHHEBVHUOEUPUQURUSVM
      WMAEKVLEVKUTVAVBVNEVCVDVNVEVFVG $.
      $( [13-Aug-2002] $)

    $( Value of supremum of set of subsets of Hilbert space.  For an alternate
       version of the value, see ~ hsupval2t . $)
    hsupvalt $p |- ( A (_ P~ H~ -> ( \/H ` A ) = ( _|_ ` ( _|_ ` U. A ) ) ) $=
      ( vx chil cpw wss chsup cfv cuni cv cch crab cint cort hsupval2t wceq
      uniss unipw syl6ss ococint syl eqtr4d ) ACDZEZAFGAHZBIEBJKLZUDMGMGZBANUCU
      DCEUFUEOUCUDUBHCAUBPCQRBUDSTUA $.
      $( [9-Dec-2003] $) $( [9-Dec-2003] $)

    $( The value of the supremum of a set of closed subspaces of Hilbert space.
       Definition of supremum in Proposition 1 of [Kalmbach] p. 65. $)
    chsupval2t $p |- ( A (_ CH ->
                    ( \/H ` A ) = |^| { x e. CH | U. A (_ x } ) $=
      ( cch wss chil cpw chsup cfv cuni cv crab cint wceq chsspwh sstr2 mpi
      hsupval2t syl ) BCDZBEFZDZBGHBIAJDACKLMSCTDUANBCTOPABQR $.
      $( [13-Aug-2002] $)

    $( The value of the supremum of a set of closed subspaces of Hilbert space.
       For an alternate version of the value, see ~ chsupval2t . $)
    chsupvalt $p |- ( A (_ CH -> ( \/H ` A ) = ( _|_ ` ( _|_ ` U. A ) ) ) $=
      ( cch wss chil cpw chsup cfv cuni cort wceq chsspwh sstr2 mpi hsupvalt
      syl ) ABCZADEZCZAFGAHIGIGJPBQCRKABQLMANO $.
      $( [13-Aug-2002] $)

    $( The span of a subset of Hilbert space is a subspace. $)
    spanclt $p |- ( A (_ H~ -> ( span ` A ) e. SH ) $=
      ( vx chil wss cspn cfv cv csh crab cint spanvalt c0 wne wa wcel wrex
      helsh sseq2 rcla4ev mpan rabn0 sylibr ssrab2 jctil shintclt syl eqeltrd
      ) ACDZAEFABGZDZBHIZJZHBAKUHUKHDZUKLMZNULHOUHUNUMUHUJBHPZUNCHOUHUOQUJUHBCH
      UICARSTUJBHUAUBUJBHUCUDUKUEUFUG $.
      $( [9-Jun-2004] $) $( [2-Jun-2004] $)

    $( A member of a span is a vector. $)
    elspanclt $p |- ( ( A (_ H~ /\ B e. ( span ` A ) ) -> B e. H~ ) $=
      ( cspn cfv csh wcel chil wss shelt spanclt sylan ) ACDZEFBLFBGFAGHBLIAJK
      $.
      $( [21-Dec-2004] $) $( [17-Dec-2004] $)

    $( Closure of the subspace supremum of set of subsets of Hilbert space. $)
    shsupclt $p |- ( A (_ P~ H~ -> ( span ` U. A ) e. SH ) $=
      ( chil cpw wss cuni cspn cfv csh wcel uniss unipw syl6ss spanclt syl ) AB
      CZDZAEZBDQFGHIPQOEBAOJBKLQMN $.
      $( [29-Nov-2004] $) $( [26-Nov-2004] $)

    $( Closure of supremum of set of subsets of Hilbert space.  Note that the
       supremum belongs to ` CH ` even if the subsets do not. $)
    hsupclt $p |- ( A (_ P~ H~ -> ( \/H ` A ) e. CH ) $=
      ( vx chil cpw wss chsup cfv cuni cv cch crab cint hsupval2t c0 wne wa
      wcel cvv fvex syl6eqelr intex sylibr ssrab2 jctil chintclt syl eqeltrd )
      ACDEZAFGZAHBIEZBJKZLZJBAMZUHUKJEZUKNOZPULJQUHUOUNUHULRQUOUHULUIRUMAFSTUKU
      AUBUJBJUCUDUKUEUFUG $.
      $( [10-Nov-1999] $)

    $( Closure of supremum of subset of ` CH ` .  Definition of supremum in
       Proposition 1 of [Kalmbach] p. 65.  Shows that ` CH ` is a complete
       lattice.  Also part of Definition 3.4-1 in [MegillPavicic] p. 2345
       (PDF p. 8). $)
    chsupclt $p |- ( A (_ CH -> ( \/H ` A ) e. CH ) $=
      ( cch wss chil cpw chsup cfv wcel chsspwh sstr2 mpi hsupclt syl ) ABCZADE
      ZCZAFGBHNBOCPIABOJKALM $.
      $( [10-Nov-1999] $)

    $( Subset relation for supremum of Hilbert space subsets. $)
    hsupss $p |- ( ( A (_ P~ H~ /\ B (_ P~ H~ ) ->
                  ( A (_ B -> ( \/H ` A ) (_ ( \/H ` B ) ) ) $=
      ( vx chil cpw wss wa chsup cfv cuni cv cch crab cint wceq hsupval2t
      adantr adantl sseq12d wi wral wcel uniss sstr2 syl a1d r19.21aiv ss2rab
      sylibr intss syl5bir ) ADEZFZBULFZGZAHIZBHIZFAJZCKZFZCLMZNZBJZUSFZCLMZNZF
      ZABFZUOUPVBUQVFUMUPVBOUNCAPQUNUQVFOUMCBPRSVHVEVAFZVGVHVDUTTZCLUAVIVHVJCLV
      HVJUSLUBVHURVCFVJABUCURVCUSUDUEUFUGVDUTCLUHUIVEVAUJUEUK $.
      $( [26-Nov-2004] $) $( [24-Nov-2004] $)

    $( Subset relation for supremum of subset of ` CH ` . $)
    chsupss $p |- ( ( A (_ CH /\ B (_ CH ) ->
                  ( A (_ B -> ( \/H ` A ) (_ ( \/H ` B ) ) ) $=
      ( chil cpw wss chsup cfv wi cch hsupss chsspwh sstr2 mpi syl2an ) ACDZEZB
      OEZABEAFGBFGEHAIEZBIEZABJRIOEZPKAIOLMSTQKBIOLMN $.
      $( [13-Aug-2002] $)

    $( A subspace is the supremum of all smaller subspaces. $)
    chsupid $p |- ( A e. CH -> ( \/H ` { x e. CH | x (_ A } ) = A ) $=
      ( vy cch wcel cv wss crab cuni cint chsup cfv unimax sseq1d rabbisdv
      inteqd intmin eqtrd wceq ssrab2 chsupval2t ax-mp syl5eq ) BDEZAFBGZADHZIZ
      CFZGZCDHZJZBUFKLZUDUKBUHGZCDHZJBUDUJUNUDUIUMCDUDUGBUHABDMNOPCBDQRUFDGULUK
      SUEADTCUFUAUBUC $.
      $( [13-Aug-2002] $)

    $( Value of supremum of subset of ` CH ` on a singleton. $)
    chsupsn $p |- ( A e. CH -> ( \/H ` { A } ) = A ) $=
      ( vx cch wcel csn chsup cfv cuni cv wss crab cint wceq snssi chsupval2t
      syl wa unisng eqimss ancli sseq2 elrab sylibr intss1 ssintub sseq1d
      mpbii eqssd eqtrd ) ACDZAEZFGZUKHZBIZJZBCKZLZAUJUKCJULUQMACNBUKOPUJUQAUJA
      UPDZUQAJUJUJUMAJZQURUJUSUJUMAMUSACRZUMASPTUOUSBACUNAUMUAUBUCAUPUDPUJUMUQJ
      AUQJBUMCUEUJUMAUQUTUFUGUHUI $.
      $( [13-Aug-2002] $)

    $( The union of a set of Hilbert space subsets is smaller than its
       supremum. $)
    hsupunss $p |- ( A (_ P~ H~ -> U. A (_ ( \/H ` A ) ) $=
      ( vx chil cpw wss cuni chsup cfv cv cch crab cint ssintub hsupval2t
      sseq2d mpbiri ) ACDEZAFZAGHZERRBIEBJKLZEBRJMQSTRBANOP $.
      $( [26-Nov-2004] $) $( [24-Nov-2004] $)

    $( A subset of Hilbert space is included in its span. $)
    spanss2 $p |- ( A (_ H~ -> A (_ ( span ` A ) ) $=
      ( vx chil wss cspn cfv cv csh crab cint ssintub spanvalt sseq2d mpbiri )
      ACDZAAEFZDAABGDBHIJZDBAHKOPQABALMN $.
      $( [13-Jun-2004] $) $( [2-Jun-2004] $)

    $( The union of a set of subspaces is smaller than its supremum. $)
    shsupunss $p |- ( A (_ SH -> U. A (_ ( span ` U. A ) ) $=
      ( csh wss cuni chil cspn cfv cpw shsspwh sstr mpan2 uniss syl unipw
      syl6ss spanss2 ) ABCZADZECRRFGCQREHZDZEQASCZRTCQBSCUAIABSJKASLMENORPM $.
      $( [29-Nov-2004] $) $( [26-Nov-2004] $)

    $( The union of a set of closed subspaces is smaller than its supremum. $)
    chsupunss $p |- ( A (_ CH -> U. A (_ ( \/H ` A ) ) $=
      ( cch wss chil cpw cuni chsup cfv chsspwh sstr mpan2 hsupunss syl ) ABCZA
      DEZCZAFAGHCNBOCPIABOJKALM $.
      $( [14-Aug-2002] $)

    $( A subspace of Hilbert space is its own span. $)
    spanid $p |- ( A e. SH -> ( span ` A ) = A ) $=
      ( vx csh wcel cspn cfv cv wss crab cint chil wceq shss spanvalt syl
      intmin eqtrd ) ACDZAEFZABGHBCIJZARAKHSTLAMBANOBACPQ $.
      $( [13-Jun-2004] $) $( [2-Jun-2004] $)

    $( Ordering relationship for the spans of subsets of Hilbert space. $)
    spanss $p |- ( ( B (_ H~ /\ A (_ B ) -> ( span ` A ) (_ ( span ` B ) ) $=
      ( vx chil wss wa cv csh crab cint cspn cfv wi wral wcel sstr2 a1d
      r19.21aiv ss2rab sylibr intss syl adantl wceq sstr ancoms spanvalt
      adantr 3sstr4d ) BDEZABEZFZACGZEZCHIZJZBUMEZCHIZJZAKLZBKLZUKUPUSEZUJUKURU
      OEZVBUKUQUNMZCHNVCUKVDCHUKVDUMHOABUMPQRUQUNCHSTURUOUAUBUCULADEZUTUPUDUKUJ
      VEABDUEUFCAUGUBUJVAUSUDUKCBUGUHUI $.
      $( [13-Jun-2004] $) $( [2-Jun-2004] $)

    $( The span of a subset of Hilbert space is less than or equal to its
       closure (double orthogonal complement). $)
    spanssoc $p |- ( A (_ H~ -> ( span ` A ) (_ ( _|_ ` ( _|_ ` A ) ) ) $=
      ( chil wss cspn cfv cort spanss ocss syl ococss sylanc csh wcel wceq
      ocsh spanid 3syl sseqtrd ) ABCZADEZAFEZFEZDEZUBUBBCZAUBCTUCCSAUBGSUABCZUD
      AHZUAHIAJKSUEUBLMUCUBNUFUAOUBPQR $.
      $( [13-Jun-2004] $) $( [3-Jun-2004] $)

    $( Value of join for subsets of Hilbert space. $)
    sshjvalt $p |- ( ( A (_ H~ /\ B (_ H~ ) -> ( A vH B ) =
        ( _|_ ` ( _|_ ` ( A u. B ) ) ) ) $=
      ( vx vy vz chil cpw wcel chj co cun cort cfv wceq wss cv fvex uneq1
      fveq2d uneq2 wa copab2 df-chj ax-hilex elpw2 anbi12i anbi1i oprabbii
      eqtr4 oprabval2 syl2anbr ) AFGZHBULHABIJABKZLMZLMZNAFOBFOCDEABULULCPZDPZK
      ZLMZLMZUOIAUQKZLMZLMUNLQUPANZUSVBLVCURVALUPAUQRSSUQBNZVBUNLVDVAUMLUQBATSS
      IUPFOZUQFOZUAZEPUTNZUAZCDEUBUPULHZUQULHZUAZVHUAZCDEUBCDEUCVMVICDEVLVGVHVJ
      VEVKVFUPFUDUEUQFUDUEUFUGUHUIUJAFUDUEBFUDUEUK $.
      $( [1-Nov-2000] $)

    $( Value of join in ` SH ` . $)
    shjvalt $p |- ( ( A e. SH /\ B e. SH ) -> ( A vH B ) =
        ( _|_ ` ( _|_ ` ( A u. B ) ) ) ) $=
      ( chil wss chj co cun cort cfv wceq csh wcel sshjvalt shss syl2an ) ACDBC
      DABEFABGHIHIJAKLBKLABMANBNO $.
      $( [9-Aug-2000] $)

    $( Value of join in ` CH ` . $)
    chjvalt $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH B ) =
        ( _|_ ` ( _|_ ` ( A u. B ) ) ) ) $=
      ( csh wcel chj co cun cort cfv wceq cch shjvalt chsh syl2an ) ACDBCDABEFA
      BGHIHIJAKDBKDABLAMBMN $.
      $( [9-Aug-2000] $)

    ${
      chjval.1 $e |- A e. CH $.
      chjval.2 $e |- B e. CH $.
      $( Value of join in ` CH ` . $)
      chjval $p |- ( A vH B ) = ( _|_ ` ( _|_ ` ( A u. B ) ) ) $=
        ( cch wcel chj co cun cort cfv wceq chjvalt mp2an ) AEFBEFABGHABIJKJKLC
        DABMN $.
        $( [9-Aug-2000] $)
    $}


    $( Alternate definition of join in the set of closed subspaces of
       Hilbert space ` CH ` . $)
    dfchj2 $p |- vH = { <. <. x , y >. , z >. | ( ( x (_ H~ /\ y (_ H~ ) /\
               z = |^| { w e. CH | ( x u. y ) (_ w } ) } $=
      ( chj cv chil wss wa cun cort cfv wceq copab2 cch crab cint df-chj unss
      ococint sylbi eqeq2d pm5.32i oprabbii eqtr ) EAFZGHBFZGHIZCFZUFUGJZKLKLZM
      ZIZABCNUHUIUJDFHDOPQZMZIZABCNABCRUMUPABCUHULUOUHUKUNUIUHUJGHUKUNMUFUGGSDU
      JTUAUBUCUDUE $.
      $( [1-Nov-2000] $)

    $( Alternate definition of join in the set of closed subspaces of Hilbert
       space ` CH ` :  the join is the supremum of its two arguments.  Based
       on the definition of join in [Beran] p. 3.  For later convenience we
       prove a general version that works for any subset of Hilbert space,
       not just the elements of the lattice ` CH ` . $)
    dfchj3 $p |- vH = { <. <. x , y >. , z >. | ( ( x (_ H~ /\ y (_ H~ ) /\
               z = ( \/H ` { x , y } ) ) } $=
      ( chj cv chil wss wa cun cort cfv wceq copab2 cpr chsup df-chj cuni cpw
      visset prsspw hsupvalt sylbir unipr fveq2i syl6req eqeq2d pm5.32i
      oprabbii eqtr ) DAEZFGBEZFGHZCEZUJUKIZJKZJKZLZHZABCMULUMUJUKNZOKZLZHZABCM
      ABCPURVBABCULUQVAULUPUTUMULUTUSQZJKZJKZUPULUSFRGUTVELUJUKFASZBSZTUSUAUBVD
      UOJVCUNJUJUKVFVGUCUDUDUEUFUGUHUI $.
      $( [3-Mar-2004] $) $( [2-Mar-2004] $)

    $( Value of join for subsets of Hilbert space in terms of supremum:  the
       join is the supremum of its two arguments.  Based on the definition of
       join in [Beran] p. 3. $)
    sshjval3t $p |- ( ( A (_ H~ /\ B (_ H~ ) -> ( A vH B ) =
                 ( \/H ` { A , B } ) ) $=
      ( vx vy vz chil cpw wcel chj co cpr chsup cfv wceq wss cv fvex preq1
      fveq2d preq2 wa copab2 dfchj3 ax-hilex elpw2 anbi12i anbi1i oprabbii
      eqtr4 oprabval2 syl2anbr ) AFGZHBULHABIJABKZLMZNAFOBFOCDEABULULCPZDPZKZLM
      ZUNIAUPKZLMUMLQUOANUQUSLUOAUPRSUPBNUSUMLUPBATSIUOFOZUPFOZUAZEPURNZUAZCDEU
      BUOULHZUPULHZUAZVCUAZCDEUBCDEUCVHVDCDEVGVBVCVEUTVFVAUOFUDUEUPFUDUEUFUGUHU
      IUJAFUDUEBFUDUEUK $.
      $( [14-Jun-2004] $) $( [2-Mar-2004] $)

    $( Closure of join for subsets of Hilbert space. $)
    sshjclt $p |- ( ( A (_ H~ /\ B (_ H~ ) -> ( A vH B ) e. CH ) $=
      ( chil wss wa chj co cun cort cfv cch sshjvalt wcel unss ocss occlt syl
      sylbi eqeltrd ) ACDBCDEZABFGABHZIJZIJZKABLTUACDZUCKMZABCNUDUBCDUEUAOUBPQR
      S $.
      $( [1-Nov-2000] $)

    $( Closure of join in ` SH ` . $)
    shjclt $p |- ( ( A e. SH /\ B e. SH ) -> ( A vH B ) e. CH ) $=
      ( chil wss chj co cch wcel csh sshjclt shss syl2an ) ACDBCDABEFGHAIHBIHAB
      JAKBKL $.
      $( [2-Nov-1999] $)

    $( Closure of join in ` CH ` . $)
    chjclt $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH B ) e. CH ) $=
      ( csh wcel chj co cch shjclt chsh syl2an ) ACDBCDABEFGDAGDBGDABHAIBIJ $.
      $( [2-Nov-1999] $)

    $( Commutative law for Hilbert lattice join of subspaces. $)
    shjcomt $p |- ( ( A e. SH /\ B e. SH ) -> ( A vH B ) = ( B vH A ) ) $=
      ( csh wcel wa chj co cun cort cfv shjvalt wceq ancoms uncom fveq2i
      syl6eq eqtr4d ) ACDZBCDZEZABFGABHZIJZIJZBAFGZABKTUDBAHZIJZIJZUCSRUDUGLBAK
      MUFUBIUEUAIBANOOPQ $.
      $( [22-Jun-2004] $) $( [22-Jun-2004] $)

    ${
      $d x y D $.
      shincl.1 $e |- A e. SH $.
      shincl.2 $e |- B e. SH $.
      $( Closure of intersection of two subspaces. $)
      shincl $p |- ( A i^i B ) e. SH $=
        ( cpr cint cin csh elisseti intpr wss c0 wne wcel wa pm3.2i prss mpbi
        prnz shintcl eqeltrr ) ABEZFABGHABAHCIZBHDIZJUBUBHKZUBLMAHNZBHNZOUEUFUG
        CDPABHUCUDQRABUCSPTUA $.
        $( [19-Oct-1999] $)

      $( Commutative law for subspace sum. $)
      shscom $p |- ( A +H B ) = ( B +H A ) $=
        ( csh wcel cph co wceq shscomt mp2an ) AEFBEFABGHBAGHICDABJK $.
        $( [17-Oct-1999] $)

      $( Vector sum belongs to subspace sum. $)
      shsva $p |- ( ( C e. A /\ D e. B ) -> ( C +h D ) e. ( A +H B ) ) $=
        ( csh wcel wa cva co cph wi shsvat mp2an ) AGHBGHCAHDBHICDJKABLKHMEFABC
        DNO $.
        $( [17-Oct-1999] $)

      $( A subspace sum contains a member of one of its subspaces. $)
      shsel1 $p |- ( C e. A -> C e. ( A +H B ) ) $=
        ( csh wcel cph co wi shsel1t mp2an ) AFGBFGCAGCABHIGJDEABCKL $.
        $( [19-Oct-1999] $)

      $( A subspace sum contains a member of one of its subspaces. $)
      shsel2 $p |- ( C e. B -> C e. ( A +H B ) ) $=
        ( csh wcel cph co wi shsel2t mp2an ) AFGBFGCBGCABHIGJDEABCKL $.
        $( [19-Oct-1999] $)

      $( Vector subtraction belongs to subspace sum. $)
      shsvs $p |- ( ( C e. A /\ D e. B ) -> ( C -h D ) e. ( A +H B ) ) $=
        ( cph co wcel cmv csh wa wi shscl shsubcltOLD ax-mp shsel1 shsel2
        syl2an ) CABGHZIZDTIZCDJHTIZCAIDBITKIUAUBLUCMABEFNCDTOPABCEFQABDEFRS $.
        $( [19-Oct-1999] $)

      $( Union is smaller than subspace sum. $)
      shunss $p |- ( A u. B ) (_ ( A +H B ) $=
        ( vx vy vz cun cph co cv wcel wo cva wceq wrex c0v chil shel
        ax-hvaddid eqcomd syl csh sh0 ax-mp rcla4eopr mp3an2 mpdan hvaddid2t
        mp3an1 jaoi elun shsel 3imtr4 ssriv ) EABHZABIJZEKZALZURBLZMURFKGKNJOGB
        PFAPZURUPLURUQLUSVAUTUSURURQNJZOZVAUSURRLZVCURACSVDVBURURTUAUBUSQBLZVCV
        ABUCLVEDBUDUEFGABURQURNUFUGUHUTURQURNJZOZVAUTVDVGURBDSVDVFURURUIUAUBQAL
        ZUTVGVAAUCLVHCAUDUEFGABQURURNUFUJUHUKURABULFGABURCDUMUNUO $.
        $( [18-Oct-1999] $)

      $( Subspace sum is smaller than Hilbert lattice join.  Remark in
         [Kalmbach] p. 65. $)
      shslej $p |- ( A +H B ) (_ ( A vH B ) $=
        ( vx vy vz cph co chj cv cva wceq wrex cun cort cfv wcel wa eleq1 csh
        wi chil wss shssi unssi ocss ax-mp ocsh shaddcltOLD elun1 ococss sseli
        syl elun2 syl2an syl5cbir r19.23aivv shsel shjvalt mp2an eleq2i 3imtr4
        ssriv ) EABHIZABJIZEKZFKZGKZLIZMZGBNFANVGABOZPQZPQZRZVGVERVGVFRVKVOFGAB
        VKVOVJVNRZVHARZVIBRZSVGVJVNTVHVNRZVIVNRZVPVQVRVNUARZVSVTSVPUBVMUCUDZWAV
        LUCUDZWBABUCACUEBDUEUFZVLUGUHVMUIUHVHVIVNUJUHVQVHVLRVSVHABUKVLVNVHWCVLV
        NUDWDVLULUHZUMUNVRVIVLRVTVIBAUOVLVNVIWEUMUNUPUQURFGABVGCDUSVFVNVGAUARBU
        ARVFVNMCDABUTVAVBVCVD $.
        $( [19-Oct-1999] $)

      $( Union is smaller than Hilbert lattice join. $)
      shunssj $p |- ( A u. B ) (_ ( A vH B ) $=
        ( cun cph co chj shunss shslej sstri ) ABEABFGABHGABCDIABCDJK $.
        $( [13-Jun-2004] $) $( [11-Jun-2004] $)

      $( Commutative law for join in ` SH ` . $)
      shjcom $p |- ( A vH B ) = ( B vH A ) $=
        ( csh wcel chj co wceq shjcomt mp2an ) AEFBEFABGHBAGHICDABJK $.
        $( [19-Oct-1999] $)

      $( Subspace sum is an upper bound of its arguments. $)
      shsub1 $p |- A (_ ( A +H B ) $=
        ( vx cph co cv shsel1 ssriv ) EAABFGABEHCDIJ $.
        $( [19-Oct-1999] $)

      $( Subspace sum is an upper bound of its arguments. $)
      shsub2 $p |- A (_ ( B +H A ) $=
        ( vx cph co cv shsel2 ssriv ) EABAFGBAEHDCIJ $.
        $( [21-Dec-2004] $) $( [17-Dec-2004] $)

      $( Hilbert lattice join is an upper bound of two subspaces. $)
      shub1 $p |- A (_ ( A vH B ) $=
        ( cph co chj shsub1 shslej sstri ) AABEFABGFABCDHABCDIJ $.
        $( [19-Oct-1999] $)

      $( Closure of ` CH ` join. $)
      shjcl $p |- ( A vH B ) e. CH $=
        ( csh wcel chj co cch shjclt mp2an ) AEFBEFABGHIFCDABJK $.
        $( [19-Oct-1999] $)

      $( ` SH ` closure of join. $)
      shjshcl $p |- ( A vH B ) e. SH $=
        ( chj co shjcl chshi ) ABEFABCDGH $.
        $( [5-May-2000] $)

      ${
        $d x y C $.
        shlub.1 $e |- C e. CH $.
        $( Hilbert lattice join is the least upper bound (among Hilbert
           lattice elements) of two subspaces. $)
        shlub $p |- ( ( A (_ C /\ B (_ C ) <-> ( A vH B ) (_ C ) $=
          ( wss wa chj co cun cort cfv chil shssi unssi chssi occon2 unss csh
          wcel wceq shjvalt mp2an pjococ eqcomi sseq12i 3imtr4 shub1 sstr mpan
          ssun2 shunssj sstri jca impbi ) ACGZBCGZHZABIJZCGZABKZCGVBLMLMZCLMLMZ
          GUSVAVBCABNADOBEOPCFQRABCSUTVCCVDATUABTUAUTVCUBDEABUCUDVDCCFUEUFUGUHV
          AUQURAUTGVAUQABDEUIAUTCUJUKBUTGVAURBVBUTBAULABDEUMUNBUTCUJUKUOUP $.
          $( [14-Jun-2004] $) $( [11-Jun-2004] $)
      $}

      ${
        shless.1 $e |- C e. SH $.
        $( Subset implies subset of subspace sum. $)
        shless $p |- ( A (_ B -> ( A +H C ) (_ ( B +H C ) ) $=
          ( vx vy vz wss cph co cv cva wceq wrex wcel wa wex ssel anim1d
          19.22dv df-rex 3imtr4g shsel ssrdv ) ABJZGACKLZBCKLZUGGMZHMZIMNLOICPZ
          HAPZULHBPZUJUHQUJUIQUGUKAQZULRZHSUKBQZULRZHSUMUNUGUPURHUGUOUQULABUKTU
          AUBULHAUCULHBUCUDHIACUJDFUEHIBCUJEFUEUDUF $.
          $( [18-Nov-2000] $)

        $( Add disjunct to both sides of Hilbert subspace ordering. $)
        shlej1 $p |- ( A (_ B -> ( A vH C ) (_ ( B vH C ) ) $=
          ( wss chj co wa shub1 sstr2 mpi shjcom sseqtr jctir shjcl shlub
          sylib ) ABGZABCHIZGZCUAGZJACHIUAGTUBUCTBUAGUBBCEFKABUALMCCBHIUACBFEKC
          BFENOPACUADFBCEFQRS $.
          $( [19-Oct-1999] $)

        $( Add disjunct to both sides of Hilbert subspace ordering. $)
        shlej2 $p |- ( A (_ B -> ( C vH A ) (_ ( C vH B ) ) $=
          ( wss chj co shlej1 shjcom 3sstr4g ) ABGACHIBCHICAHICBHIABCDEFJCAFDKC
          BFEKL $.
          $( [19-Oct-1999] $)
      $}
    $}
  $}

  $( Subspace sum is smaller than subspace join.  Remark in [Kalmbach]
     p. 65. $)
  shslejt $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) (_ ( A vH B ) ) $=
    ( csh wcel cph co chj wss chil cif wceq opreq1 sseq12d opreq2 helsh elimel
    shslej dedth2h ) ACDZBCDZABEFZABGFZHSAIJZBEFZUCBGFZHUCTBIJZEFZUCUFGFZHABIIA
    UCKUAUDUBUEAUCBELAUCBGLMBUFKUDUGUEUHBUFUCENBUFUCGNMUCUFAICOPBICOPQR $.
    $( [26-Aug-2004] $) $( [12-Jul-2004] $)

  $( Closure of intersection of two subspaces. $)
  shinclt $p |- ( ( A e. SH /\ B e. SH ) -> ( A i^i B ) e. SH ) $=
    ( csh wcel cin chil cif wceq ineq1 eleq1d ineq2 helsh elimel shincl
    dedth2h ) ACDZBCDZABEZCDPAFGZBEZCDSQBFGZEZCDABFFASHRTCASBIJBUAHTUBCBUASKJSU
    AAFCLMBFCLMNO $.
    $( [24-Jun-2004] $) $( [24-Jun-2004] $)

  $( Hilbert lattice join is an upper bound of two subspaces. $)
  shub1t $p |- ( ( A e. SH /\ B e. SH ) -> A (_ ( A vH B ) ) $=
    ( csh wcel wa cph co chj shsub1t shslejt sstrd ) ACDBCDEAABFGABHGABIABJK $.
    $( [22-Jun-2004] $) $( [22-Jun-2004] $)

  $( A subspace is a subset of its Hilbert lattice join with another. $)
  shub2t $p |- ( ( A e. SH /\ B e. SH ) -> A (_ ( B vH A ) ) $=
    ( csh wcel wa chj co shub1t shjcomt sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
    $( [23-Jun-2004] $) $( [22-Jun-2004] $)

  $( Hilbert lattice join is the least upper bound (among Hilbert lattice
     elements) of two subspaces. $)
  shlubt $p |- ( ( A e. SH /\ B e. SH /\ C e. CH ) ->
               ( ( A (_ C /\ B (_ C ) <-> ( A vH B ) (_ C ) ) $=
    ( csh wcel cch wss wa chj co wb chil cif wceq sseq1 anbi1d opreq1 sseq1d
    bibi12d anbi2d opreq2 sseq2 anbi12d helsh elimel helch shlub dedth3h ) ADEZ
    BDEZCFEZACGZBCGZHZABIJZCGZKUIALMZCGZUMHZUQBIJZCGZKABCLLLURUJBLMZCGZHZUQVBIJ
    ZCGZKUQUKCLMZGZVBVGGZHZVEVGGZKAUQNZUNUSUPVAVLULURUMAUQCOPVLUOUTCAUQBIQRSBVB
    NZUSVDVAVFVMUMVCURBVBCOTVMUTVECBVBUQIUARSCVGNZVDVJVFVKVNURVHVCVICVGUQUBCVGV
    BUBUCCVGVEUBSUQVBVGALDUDUEBLDUDUECLFUFUEUGUH $.
    $( [15-Jun-2004] $) $( [15-Jun-2004] $)

  $( Add disjunct to both sides of Hilbert subspace ordering. $)
  shlej1t $p |- ( ( ( A e. SH /\ B e. SH /\ C e. SH ) /\
               A (_ B ) -> ( A vH C ) (_ ( B vH C ) ) $=
    ( csh wcel w3a wss chj co wa wi sstr2 shub1t syl5com shub2t ancoms jctird
    3adant1 cch wb shlubt 3simp1 3simp3 shjclt syl3anc sylibd imp ) ADEZBDEZCDE
    ZFZABGZACHIBCHIZGZUKULAUMGZCUMGZJZUNUIUJULUQKUHUIUJJZULUOUPULBUMGUOURABUMLB
    CMNUJUIUPCBOPQRUHUJUMSEZUQUNTUKACUMUAUHUIUJUBUHUIUJUCUIUJUSUHBCUDRUEUFUG $.
    $( [27-Dec-2006] $) $( [22-Jun-2004] $)

  $( Add disjunct to both sides of Hilbert subspace ordering. $)
  shlej2t $p |- ( ( ( A e. SH /\ B e. SH /\ C e. SH ) /\
               A (_ B ) -> ( C vH A ) (_ ( C vH B ) ) $=
    ( csh wcel w3a wss wa chj co shlej1t wceq shjcomt 3adant2 adantr 3adant1
    3sstr3d ) ADEZBDEZCDEZFZABGZHACIJZBCIJZCAIJZCBIJZABCKUAUCUELZUBRTUGSACMNOUA
    UDUFLZUBSTUHRBCMPOQ $.
    $( [3-Jan-2007] $) $( [22-Jun-2004] $)

  ${
    $d x y z A $.
    shsidm.1 $e |- A e. SH $.
    $( Idempotent law for Hilbert subspace sum. $)
    shsidm $p |- ( A +H A ) = A $=
      ( vx vy vz cph co cv wcel cva wceq wrex shsel wa eleq1 csh wi
      shaddcltOLD ax-mp syl5cbir r19.23aivv sylbi ssriv shsub1 eqssi ) AAFGZACU
      FACHZUFIUGDHZEHZJGZKZEALDALUGAIZDEAAUGBBMUKULDEAAUKULUJAIZUHAIUIAINZUGUJA
      OAPIUNUMQBUHUIARSTUAUBUCAABBUDUE $.
      $( [14-Jun-2004] $) $( [6-Jun-2004] $)
  $}

  ${
    shslub.1 $e |- A e. SH $.
    shslub.2 $e |- B e. SH $.
    shslub.3 $e |- C e. SH $.
    $( Least upper bound law for Hilbert subspace sum. $)
    shslub $p |- ( ( A (_ C /\ B (_ C ) <-> ( A +H B ) (_ C ) $=
      ( wss wa cph co shless shscom syl6ss shsidm sylan9ss shsub1 sstr mpan
      shsub2 jca impbi ) ACGZBCGZHABIJZCGZUBUCUDBCIJZCUBUDCBIJUFACBDFEKCBFELMUC
      UFCCIJCBCCEFFKCFNMOUEUBUCAUDGUEUBABDEPAUDCQRBUDGUEUCBAEDSBUDCQRTUA $.
      $( [15-Jun-2004] $) $( [15-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    shlesb1.1 $e |- A e. SH $.
    shlesb1.2 $e |- B e. SH $.
    $( Hilbert lattice ordering in terms of subspace sum. $)
    shlesb1 $p |- ( A (_ B <-> ( A +H B ) = B ) $=
      ( wss wa cph co wceq ssid biantrur shslub eqss shsub2 mpbiran2 shscom
      sseq1i bitr2 3bitr ) ABEZBBEZTFBAGHZBEZABGHZBIZUATBJKBABDCDLUEUDBEZUCUEUF
      BUDEUDBMBADCNOUDUBBABCDPQRS $.
      $( [29-Nov-2004] $) $( [23-Nov-2004] $)

    $( An alternate way to express subspace sum. $)
    shsumval2 $p |- ( A +H B ) = |^| { x e. SH | ( A u. B ) (_ x } $=
      ( cph co cun cv wss csh crab cint wa ssun1 ssintub sstri ssun2 pm3.2i c0
      wne wcel ssrab2 wrex shscl shunss sseq2 rcla4ev mp2an rabn0 mpbir
      shintclt shslub mpbi elrab mpbir2an intss1 ax-mp eqssi ) BCFGZBCHZAIZJZAK
      LZMZBVEJZCVEJZNUTVEJVFVGBVAVEBCOAVAKPZQCVAVECBRVHQSBCVEDEVDKJVDTUAZVEKUBV
      CAKUCVIVCAKUDZUTKUBZVAUTJZVJBCDEUEZBCDEUFZVCVLAUTKVBUTVAUGZUHUIVCAKUJUKVD
      ULUIUMUNUTVDUBZVEUTJVPVKVLVCVLAUTKVOUOVMVNUPUTVDUQURUS $.
      $( [2-Dec-2004] $) $( [25-Nov-2004] $)

    $( An alternate way to express subspace sum. $)
    shsumval3 $p |- ( A +H B ) = ( span ` ( A u. B ) ) $=
      ( vx cph co cun cv wss csh crab cint cspn cfv shsumval2 chil wceq shssi
      unssi spanvalt ax-mp eqtr4 ) ABFGABHZEIJEKLMZUDNOZEABCDPUDQJUFUERABQACSBD
      STEUDUAUBUC $.
      $( [2-Dec-2004] $) $( [25-Nov-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    shmod.1 $e |- A e. SH $.
    shmod.2 $e |- B e. SH $.
    shmod.3 $e |- C e. SH $.
    $( The modular law holds for subspace sum.  Similar to part of Theorem 16.9
       of [MaedaMaeda] p. 70. $)
    shmods $p |- ( A (_ C -> ( ( A +H B ) i^i C ) (_ ( A +H ( B i^i C ) ) ) $=
      ( vz vx vy wss cph co cin cv wcel wa cva wceq wrex wi cmv wb w3a chil
      hvsubaddt shel syl3an eqcom syl6bb 3expb shlesb1 biimp eleq2d shsvs
      shscom syl6eleq syl5bi eleq1 biimpd sylan9 anim2d elin syl6ibr ex com13
      ancoms anasss sylbird imp shincl shsva syl5bir exp3a com12 ad2antrl syld
      exp31 r19.23advv shsel syl5ib imp3a ssrdv ) ACJZGABKLZCMZABCMZKLZWCGNZWDO
      ZWHCOZPWHWGOZWHWEOWCWIWJWKWJWIWCWKWJWHHNZINZQLZRZIBSHASWCWKTZWIWJWOWPHIAB
      WJWLAOZWMBOZPZWOWPWJWSPZWOPWCWMWFOZWKWTWOWCXATZWTWOWHWLUALZWMRZXBWJWQWRXD
      WOUBWJWQWRUCXDWNWHRZWOWHUDOWLUDOWMUDOXDXEUBWJWQWRWHWLWMUEWHCFUFWLADUFWMBE
      UFUGWNWHUHUIUJWJWQWRXDXBTZWRWJWQPZXFWCXDWRXGPZXAWCXDXHXATWCXDPZXHWRWMCOZP
      XAXIXGXJWRWCXGXCCOZXDXJWCXCACKLZOXKXGWCXLCXCWCXLCRACDFUKULUMXGXCCAKLXLCAW
      HWLFDUNCAFDUOUPUQXDXKXJXCWMCURUSUTVAWMBCVBVCVDVEVFVGVHVIWTWOXAWKTZWQWOXMT
      WJWRWOWQXMWOWQXAWKWOWKWNWGOWQXAPWHWNWGURAWFWLWMDBCEFVJVKVLVMVNVOVIVPVQVRH
      IABWHDEVSVTVEWAWHWDCVBVTWB $.
      $( [2-Dec-2004] $) $( [23-Nov-2004] $)

    $( The modular law is implied by the closure of subspace sum.  Part of
       proof of Theorem 16.9 of [MaedaMaeda] p. 70. $)
    shmod $p |- ( ( ( A +H B ) = ( A vH B ) /\ A (_ C ) ->
                ( ( A vH B ) i^i C ) (_ ( A vH ( B i^i C ) ) ) $=
      ( cph co chj wceq wss wa cin ineq1 sseq1d shmods syl5bi imp shincl
      shslej sstr mpan2 syl ) ABGHZABIHZJZACKZLUECMZABCMZGHZKZUHAUIIHZKZUFUGUKU
      FUDCMZUJKUKUGUFUNUHUJUDUECNOABCDEFPQRUKUJULKUMAUIDBCEFSTUHUJULUAUBUC $.
      $( [4-Dec-2004] $) $( [23-Nov-2004] $)
  $}

