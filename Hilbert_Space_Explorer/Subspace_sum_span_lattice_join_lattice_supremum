$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Subspace sum, span, lattice join, lattice supremum
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u $.
    $( Define subspace sum in ` SH ` .  See ~ shsumval , ~ shsumval2i , and
       ~ shsumval3i for its value. $)
    df-shsum $a |- +H = { <. <. x , y >. , z >. | ( ( x e. SH /\ y e. SH ) /\
                 z = { w e. ~H | E. v e. x E. u e. y w = ( v +h u ) } ) } $.

    $( Define the linear span of a subset of Hilbert space.  Definition of span
       in [Schechter] p. 276.  See ~ spanval for its value. $)
    df-span $a |- span = { <. x , y >. | ( x C_ ~H /\
                   y = |^| { z e. SH | x C_ z } ) } $.

    $( Define Hilbert lattice join.  See ~ chjval for its value and ~ chjcl for
       its closure law.  Note that we define it over all Hilbert space subsets
       to allow proving more general theorems.  Even for general subsets the
       join belongs to ` CH ` ; see ~ sshjcl .  For an alternate definition see
       ~ dfchj2 . $)
    df-chj $a |- vH = { <. <. x , y >. , z >. | ( ( x C_ ~H /\ y C_ ~H ) /\
               z = ( _|_ ` ( _|_ ` ( x u. y ) ) ) ) } $.

    $( Define the supremum of a set of Hilbert lattice elements.  See
       ~ chsupval2 for its value and ~ dfchsup2 for an alternate definition.
       We actually define the supremum for an arbitrary collection of Hilbert
       space subsets, not just elements of the Hilbert lattice ` CH ` , to
       allow more general theorems.  Even for general subsets the supremum
       still a Hilbert lattice element; see ~ hsupcl . $)
    df-chsup $a |- \/H = { <. x , y >. | ( x C_ ~P ~H /\
                   y = ( _|_ ` ( _|_ ` U. x ) ) ) } $.

  $}

  ${
    $d x y z w f g v u A $.  $d x y z w f g v u B $.  $d x y z C $.
    $d x y D $.
    $( Value of subspace sum of two Hilbert space subspaces.  Definition of
       subspace sum in [Kalmbach] p. 65. $)
    shsumval $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) =
        { x e. ~H | E. y e. A E. z e. B x = ( y +h z ) } ) $=
      ( vw vv vu csh cv cva co wceq wrex chil crab cph ax-hilex rabex rexeq
      rabbidv rexbidv df-shsum oprabval2 ) FGHDEIIAJBJCJKLMZCGJZNZBFJZNZAOPUECE
      NZBDNZAOPQUGBDNZAOPUKAORSUHDMUIULAOUGBUHDTUAUFEMZULUKAOUMUGUJBDUECUFETUBU
      AFGHABCUCUD $.
      $( [16-Oct-1999] $)

    $( Membership in the subspace sum of two Hilbert subspaces. $)
    shsel $p |- ( ( A e. SH /\ B e. SH ) -> ( C e. ( A +H B ) <->
                 E. x e. A E. y e. B C = ( x +h y ) ) ) $=
      ( vz csh wcel wa cph co chil cv cva wceq wrex crab shsumval eleq2d eqeq1
      2rexbidv elrab syl6bb wi shss sseld im2anan9 hvaddcl eleq1a syl syl6
      r19.23advv pm4.71rd bitr4d ) CGHZDGHZIZECDJKZHZELHZEAMZBMZNKZOZBDPACPZIZV
      EUQUSEFMZVCOZBDPACPZFLQZHVFUQURVJEFABCDRSVIVEFELVGEOVHVDABCDVGEVCTUAUBUCU
      QVEUTUQVDUTABCDUQVACHZVBDHZIVALHZVBLHZIZVDUTUDZUOVKVMUPVLVNUOCLVACUEUFUPD
      LVBDUEUFUGVOVCLHVPVAVBUHVCLEUIUJUKULUMUN $.
      $( [14-Dec-2004] $)

    $( Membership in the subspace sum of two Hilbert subspaces, using vector
       subtraction. $)
    shsel3 $p |- ( ( A e. SH /\ B e. SH ) -> ( C e. ( A +H B ) <->
                 E. x e. A E. y e. B C = ( x -h y ) ) ) $=
      ( vz csh wcel wa cph co cv cva wceq wrex cmv shsel c1 cneg csm id chil
      hvaddsubval shel syl2an an4s anassrs sylan9eqr opreq2 eqeq2d rcla4ev cc
      ax1cn negcli shmulcl mp3an2 adantll adantlr sylan syldan ex r19.23adva
      hvsubval impbid rexbidva bitrd ) CGHZDGHZIZECDJKHEALZFLZMKZNZFDOZACOEVJBL
      ZPKZNZBDOZACOAFCDEQVIVNVRACVIVJCHZIZVNVRVTVMVRFDVTVKDHZIZVMVRWBVMEVJRSZVK
      TKZPKZNZVRVMWBEVLWEVMUAVIVSWAVLWENZVGVSVHWAWGVJUBHZVKUBHWGVGVSIZVHWAIVJVK
      UCVJCUDZVKDUDUEUFUGUHWDDHZWFVRWBVQWFBWDDVOWDNVPWEEVOWDVJPUIUJUKVIWAWKVSVH
      WAWKVGVHWCULHZWAWKRUMUNZWCVKDUOUPUQURUSUTVAVBVTVQVNBDVTVODHZIZVQVNWOVQEVJ
      WCVOTKZMKZNZVNVQWOEVPWQVQUAVIVSWNVPWQNZVGVSVHWNWSWHVOUBHWSWIVHWNIVJVOVCWJ
      VODUDUEUFUGUHWPDHZWRVNWOVMWRFWPDVKWPNVLWQEVKWPVJMUIUJUKVIWNWTVSVHWNWTVGVH
      WLWNWTWMWCVODUOUPUQURUSUTVAVBVDVEVF $.
      $( [20-Jan-2007] $)

    ${
      shscl.1 $e |- A e. SH $.
      shscl.2 $e |- B e. SH $.
      $( Membership in subspace sum. $)
      shseli $p |- ( C e. ( A +H B )
                 <-> E. x e. A E. y e. B C = ( x +h y ) ) $=
        ( csh wcel cph co cv cva wceq wrex wb shsel mp2an ) CHIDHIECDJKIEALBLMK
        NBDOACOPFGABCDEQR $.
        $( [4-May-2000] $)

      $( Closure of subspace sum. $)
      shscli $p |- ( A +H B ) e. SH $=
        ( vx vy vz vw vv vu vf vg cph co csh wcel chil wss c0v wa cv cva wral
        csm cc sh wceq wrex crab shsumval mp2an ssrab2 eqsstri sh0 ax-mp
        ax-hv0cl hvaddid2i eqcomi rcla4eopr mp3an shseli mpbir pm3.2i wi
        shaddclOLD ad2ant2r ad2ant2l opreq12 hvadd4 sheli anim12i syl2an an4s
        eqtr4d syl111anc ancoms exp43 r19.23aivv com3l imp anbi12i 3imtr4i
        rgen2a shmulcl mp3an1 adantrr adantrl opreq2 adantl ad2antll
        ax-hvdistr1 id syl3an 3expb adantrrr eqtrd exp42 impcom anbi2i rgen2
        mpbir2an ) ABMNZOPXBQRZSXBPZTEUAZFUAZUBNZXBPZFXBUCEXBUCZXEXFUDNZXBPZFXB
        UCEUEUCZTEFXBUFXCXDXBGUAZXGUGFBUHEAUHZGQUIZQAOPZBOPZXBXOUGCDGEFABUJUKXN
        GQULUMXDSXGUGFBUHEAUHZSAPZSBPZSSSUBNZUGXRXPXSCAUNUOXQXTDBUNUOYASSUPUQUR
        EFABSSSUBUSUTEFABSCDVAVBVCXIXLXHEFXBXEXMHUAZUBNZUGZHBUHGAUHZXFIUAZJUAZU
        BNZUGZJBUHIAUHZTXGKUALUAUBNZUGLBUHKAUHZXEXBPZXFXBPZTXHYEYJYLYDYJYLVDGHA
        BYJXMAPZYBBPZTZYDYLYIYQYDYLVDVDIJABYFAPZYGBPZTZYIYQYDYLYQYDTZYTYITZYLUU
        AUUBTZXMYFUBNZAPZYBYGUBNZBPZXGUUDUUFUBNZUGYLYQYTUUEYDYIYOYRUUEYPYSXPYOY
        RTZUUEVDCXMYFAVEUOVFVFYQYTUUGYDYIYPYSUUGYOYRXQYPYSTZUUGVDDYBYGBVEUOVGVF
        UUCXGYCYHUBNZUUHYDYIXGUUKUGYQYTXEYCXFYHUBVHVGYQYTUUHUUKUGZYDYIYOYRYPYSU
        ULXMQPZYFQPZTYBQPZYGQPZTUULUUIUUJXMYFYBYGVIYOUUMYRUUNXMACVJYFACVJZVKYPU
        UOYSUUPYBBDVJYGBDVJZVKVLVMVFVNKLABUUDUUFXGUBUSVOVPVQVRVSVRVTYMYEYNYJGHA
        BXECDVAIJABXFCDVAZWAKLABXGCDVAWBWCXKEFUEXBXEUEPZYJTXJYKUGLBUHKAUHZUUTYN
        TXKYJUUTUVAYIUUTUVAVDZIJABYRYSYIUVBVDYRYSYIUUTUVAUUTYRYSYITZTZUVAUUTUVD
        TZXEYFUDNZAPZXEYGUDNZBPZXJUVFUVHUBNZUGUVAUUTYRUVGUVCXPUUTYRUVGCXEYFAWDW
        EWFUUTUVCUVIYRUUTYSUVIYIXQUUTYSUVIDXEYGBWDWEWFWGUVEXJXEYHUDNZUVJUVCXJUV
        KUGZUUTYRYIUVLYSXFYHXEUDWHWIWJUUTYRYSUVKUVJUGZYIUUTYRYSUVMUUTUUNUUPUVMU
        UTYRYSXEYFYGWKUUTWLUUQUURWMWNWOWPKLABUVFUVHXJUBUSVOVPWQVTVRWRYNYJUUTUUS
        WSKLABXJCDVAWBWTVCXA $.
        $( [15-Oct-1999] $)
    $}

    $( Closure of subspace sum. $)
    shscl $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) e. SH ) $=
      ( csh wcel cph co chil cif wceq opreq1 eleq1d opreq2 helsh elimel shscli
      dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCC
      BUBTELKTUBAGCMNBGCMNOP $.
      $( [15-Dec-2004] $)

    $( Commutative law for subspace sum. $)
    shscom $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) = ( B +H A ) ) $=
      ( vx vy vz csh wcel wa cph co cv cva wceq wrex chil shel anim12i an4s
      ax-hvcom syl eqeq2d 2rexbidva rexcom syl6bb shsel wb ancoms 3bitr4d
      eqrdv ) AFGZBFGZHZCABIJZBAIJZULCKZDKZEKZLJZMZEBNDANZUOUQUPLJZMZDANEBNZUOU
      MGUOUNGZULUTVBEBNDANVCULUSVBDEABULUPAGZUQBGZHHZURVAUOVGUPOGZUQOGZHZURVAMU
      JVEUKVFVJUJVEHVHUKVFHVIUPAPUQBPQRUPUQSTUAUBVBDEABUCUDDEABUOUEUKUJVDVCUFED
      BAUOUEUGUHUI $.
      $( [15-Dec-2004] $)

    $( Vector sum belongs to subspace sum. $)
    shsva $p |- ( ( A e. SH /\ B e. SH ) -> ( ( C e. A /\ D e. B ) ->
                 ( C +h D ) e. ( A +H B ) ) ) $=
      ( vx vy csh wcel wa cva co cph cv wceq wrex shsel eqid rcla4eopr mp3an3
      syl5bir ) AGHBGHICDJKZABLKHUAEMFMJKNFBOEAOZCAHZDBHZIEFABUAPUCUDUAUANUBUAQ
      EFABCDUAJRST $.
      $( [15-Dec-2004] $)

    $( A subspace sum contains a member of one of its subspaces. $)
    shsel1 $p |- ( ( A e. SH /\ B e. SH ) ->
                  ( C e. A -> C e. ( A +H B ) ) ) $=
      ( csh wcel wa cph co c0v cva wceq chil shel ax-hvaddid syl adantlr sh0
      adantl shsva mpan2d imp eqeltrrd ex ) ADEZBDEZFZCAEZCABGHZEUFUGFCIJHZCUHU
      DUGUICKZUEUDUGFCLEUJCAMCNOPUFUGUIUHEZUFUGIBEZUKUEULUDBQRABCISTUAUBUC $.
      $( [15-Dec-2004] $)

    $( A subspace sum contains a member of one of its subspaces. $)
    shsel2 $p |- ( ( A e. SH /\ B e. SH ) ->
                 ( C e. B -> C e. ( A +H B ) ) ) $=
      ( csh wcel wa cph co wi shsel1 ancoms shscom eleq2d sylibrd ) ADEZBDEZFZC
      BEZCBAGHZEZCABGHZEPORTIBACJKQUASCABLMN $.
      $( [15-Dec-2004] $)

    $( Vector subtraction belongs to subspace sum. $)
    shsvs $p |- ( ( A e. SH /\ B e. SH ) ->
                ( ( C e. A /\ D e. B ) -> ( C -h D ) e. ( A +H B ) ) ) $=
      ( csh wcel wa cph co cmv wi shscl shsubclOLD syl shsel1 shsel2 syl2and )
      AEFBEFGZCABHIZFZDSFZCDJISFZCAFDBFRSEFTUAGUBKABLCDSMNABCOABDPQ $.
      $( [15-Dec-2004] $)

    $( Subspace sum is an upper bound of its arguments. $)
    shsub1 $p |- ( ( A e. SH /\ B e. SH ) -> A C_ ( A +H B ) ) $=
      ( vx csh wcel wa cph co cv shsel1 ssrdv ) ADEBDEFCAABGHABCIJK $.
      $( [14-Dec-2004] $)

    $( Subspace sum is an upper bound of its arguments. $)
    shsub2 $p |- ( ( A e. SH /\ B e. SH ) -> A C_ ( B +H A ) ) $=
      ( csh wcel wa cph co shsub1 shscom sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
      $( [17-Dec-2004] $)

    $( The orthocomplement of the zero subspace is the unit subspace. $)
    choc0 $p |- ( _|_ ` 0H ) = ~H $=
      ( vx vy c0h cort cfv chil cv wcel csp co cc0 wceq wral wa wi abai csh wb
      h0elsh shocel ax-mp c0v hi02 wal df-ral elch0 imbi1i albii ax-hv0cl
      elisseti opreq2 eqeq1d ceqsalv 3bitri sylibr biantru 3bitr4i eqriv ) ACDE
      ZFAGZFHZUTBGZIJZKLZBCMZNZVAVAVEOZNUTUSHZVAVAVEPCQHVHVFRSBUTCTUAVGVAVAUTUB
      IJZKLZVEUTUCVEVBCHZVDOZBUDVBUBLZVDOZBUDVJVDBCUEVLVNBVKVMVDVBUFUGUHVDVJBUB
      UBFUIUJVMVCVIKVBUBUTIUKULUMUNUOUPUQUR $.
      $( [15-Oct-1999] $)

    $( The orthocomplement of the unit subspace is the zero subspace.  Does not
       require Axiom of Choice. $)
    choc1 $p |- ( _|_ ` ~H ) = 0H $=
      ( vx vy chil cort cfv c0h cv wcel c0v wceq csp co cc0 wral csh wa wb
      helsh shocel ax-mp simprbi wss shocss sseli hial0 syl mpbid elch0 sylibr
      ssriv h0elsh shococss choc0 fveq2i sseqtri eqssi ) CDEZFAUQFAGZUQHZURIJZU
      RFHUSURBGKLMJBCNZUTUSURCHZVACOHZUSVBVAPQRBURCSTUAUSVBVAUTQUQCURVCUQCUBRCU
      CTUDBURUEUFUGURUHUIUJFFDEZDEZUQFOHFVEUBUKFULTVDCDUMUNUOUP $.
      $( [24-Oct-1999] $)

    ${
      $d x y $.
      $( Orthogonal complement of the empty set. $)
      chocnul $p |- ( _|_ ` (/) ) = ~H $=
        ( vx vy c0 cort cfv chil cv wcel csp co cc0 wceq wral wss wa wb 0ss
        ocel ax-mp ral0 mpbiran2 eqriv ) ACDEZFAGZUCHZUDFHZUDBGIJKLZBCMZCFNUEUF
        UHOPFQBUDCRSUGBTUAUB $.
        $( [31-Oct-2000] $)
    $}

    ${
      shintcl.1 $e |- ( A C_ SH /\ A =/= (/) ) $.
      $( Closure of intersection of a non-empty subset of ` SH ` . $)
      shintcli $p |- |^| A e. SH $=
        ( vx vy vz cint csh wcel chil wss c0v wa cv cva co wral csm cc sh c0
        wne simpri wex n0 intss1 simpli sseli shss syl sstrd 19.23aiv sylbi
        ax-mp ax-hv0cl elisseti elint2 sh0 mprgbir pm3.2i shaddcl syl3an1
        3expib elinti com12 syl2and r19.21aiv oprex sylibr rgen2a shmulcl
        sylan2d rgen2 mpbir2an ) AFZGHVNIJZKVNHZLCMZDMZNOZVNHZDVNPCVNPZVQVRQOZV
        NHZDVNPCRPZLCDVNSVOVPATUAZVOAGJZWEBUBWEEMZAHZEUCVOEAUDWHVOEWHVNWGIWGAUE
        WHWGGHZWGIJAGWGWFWEBUFUGZWGUHUIUJUKULUMVPKWGHZEAEKAKIUNUOUPWHWIWKWJWGUQ
        UIURUSWAWDVTCDVNVQVNHZVRVNHZLZVSWGHZEAPVTWNWOEAWHWNWOWHVQWGHZVRWGHZWOWL
        WMWHWPWQWOWIWPWQWOWHVQVRWGUTWJVAVBWLWHWPVQAWGVCVDWMWHWQVRAWGVCVDZVEVDVF
        EVSAVQVRNVGUPVHVIWCCDRVNVQRHZWMLZWBWGHZEAPWCWTXAEAWHWTXAWHWSWQXAWMWHWSW
        QXAWIWSWQXAWHVQVRWGVJWJVAVBWRVKVDVFEWBAVQVRQVGUPVHVLUSVM $.
        $( [19-Jan-2007] $) $( [14-Oct-1999] $)
    $}

    $( The intersection of a non-empty set of subspaces is a subspace. $)
    shintcl $p |- ( ( A C_ SH /\ A =/= (/) ) -> |^| A e. SH ) $=
      ( csh wss c0 wne wa cint wcel cif wceq inteq eleq1d sseq1 neeq1 anbi12d
      ssid c0h h0elsh ne0i ax-mp pm3.2i elimhyp shintcli dedth ) ABCZADEZFZAGZB
      HUGABIZGZBHABAUIJZUHUJBAUIKLUIUGUIBCZUIDEZFBBCZBDEZFABUKUEULUFUMAUIBMAUID
      NOBUIJUNULUOUMBUIBMBUIDNOUNUOBPQBHUORBQSTUAUBUCUD $.
      $( [20-May-2007] $) $( [2-Jun-2004] $)

    ${
      chintcl.1 $e |- ( A C_ CH /\ A =/= (/) ) $.
      $( The intersection of a non-empty set of closed subspaces is a closed
         subspace. $)
      chintcli $p |- |^| A e. CH $=
        ( vf vx vy cint cch wcel csh cn cv wf chli wbr wa wi wal closedsub wss
        c0 wne simpli chsssh sstri simpri pm3.2i shintcli wral visset chlimi
        3exp com3r sseli syl5 imp ralimdva fint elint2 3imtr4g impcom gen2
        mpbir2an ) AFZGHVCIHJVCCKZLZVDDKZMNZOVFVCHZPZDQCQDCVCRAAISATUAZAGIAGSZV
        JBUBZUCUDVKVJBUEZUFUGVICDVGVEVHVGJEKZVDLZEAUHVFVNHZEAUHVEVHVGVOVPEAVGVN
        AHZVOVPPZVGVNGHZVRVQVSVOVGVPVSVOVGVPVFVDVNDUIZUJUKULAGVNVLUMUNUOUPEJAVD
        VMUQEVFAVTURUSUTVAVB $.
        $( [23-Jan-2007] $) $( [14-Oct-1999] $)
    $}

    $( The intersection (infimum) of a non-empty subset of ` CH ` belongs to
       ` CH ` .  Part of Theorem 3.13 of [Beran] p. 108.  Also part of
       Definition 3.4-1 in [MegPav2000] p. 2345 (PDF p. 8). $)
    chintcl $p |- ( ( A C_ CH /\ A =/= (/) ) -> |^| A e. CH ) $=
      ( cch wss c0 wne wa cint wcel cif wceq inteq eleq1d sseq1 neeq1 anbi12d
      ssid c0h h0elch ne0i ax-mp pm3.2i elimhyp chintcli dedth ) ABCZADEZFZAGZB
      HUGABIZGZBHABAUIJZUHUJBAUIKLUIUGUIBCZUIDEZFBBCZBDEZFABUKUEULUFUMAUIBMAUID
      NOBUIJUNULUOUMBUIBMBUIDNOUNUOBPQBHUORBQSTUAUBUCUD $.
      $( [26-Feb-2007] $) $( [14-Oct-1999] $)

    $( The double complement is the smallest closed subspace containing a
       subset of Hilbert space.  Remark 3.12(B) of [Beran] p. 107. $)
    ococin $p |- ( A C_ ~H ->
                  ( _|_ ` ( _|_ ` A ) ) = |^| { x e. CH | A C_ x } ) $=
      ( chil wss cort cfv cv cch crab cint wa wcel helch jctl sseq2 elrab
      sylibr intss1 ocss 3syl jca ssintub wi syl occon mpdan mpi sylc c0 wne
      wceq wrex rcla4ev mpan rabn0 ssrab2 jctil chintcl ococ sseqtrd occl
      ococss sylanbrc eqssd ) BCDZBEFZEFZBAGZDZAHIZJZVEVGVKEFZEFZVKVEVLCDZVFCDZ
      KVLVFDZVGVMDVEVNVOVECVJLZVKCDZVNVECHLZVEKVQVEVSMNVIVEACHVHCBOZPQZCVJRZVKS
      TBSZUAVEBVKDZVPABHUBVEVRWDVPUCVEVQVRWAWBUDBVKUEUFUGVLVFUEUHVEVJHDZVJUIUJZ
      KVKHLVMVKUKVEWFWEVEVIAHULZWFVSVEWGMVIVEACHVTUMUNVIAHUOQVIAHUPUQVJURVKUSTU
      TVEVGVJLZVKVGDWHVGHLZBVGDZVEVIWJAVGHVHVGBOPVEVOWIWCVFVAUDBVBVCVGVJRUDVD
      $.
      $( [8-Aug-2000] $)

    $( Alternate definition of supremum of a subset of the Hilbert lattice.
       Definition in Proposition 1 of [Kalmbach] p. 65.  We actually define it
       on any collection of Hilbert space subsets, not just the Hilbert lattice
       ` CH ` , to allow more general theorems. $)
    dfchsup2 $p |- \/H = { <. x , y >. | ( x C_ ~P ~H /\
                   y = |^| { z e. CH | U. x C_ z } ) } $=
      ( chsup cv chil cpw wss cuni cort cfv wceq wa copab cch crab cint
      df-chsup uniss unipw syl6ss ococin syl eqeq2d pm5.32i opabbii eqtri ) DAE
      ZFGZHZBEZUHIZJKJKZLZMZABNUJUKULCEHCOPQZLZMZABNABRUOURABUJUNUQUJUMUPUKUJUL
      FHUMUPLUJULUIIFUHUISFTUACULUBUCUDUEUFUG $.
      $( [9-Dec-2003] $)

    $( Value of the linear span of a subset of Hilbert space.  The span is the
       intersection of all subspaces constraining the subset.  Definition of
       span in [Schechter] p. 276. $)
    spanval $p |- ( A C_ ~H -> ( span ` A ) = |^| { x e. SH | A C_ x } ) $=
      ( vy vz chil wss cpw wcel cspn cfv cv csh crab cint wceq ax-hilex elpw2
      cvv wrex helsh sseq2 rcla4ev mpan sylbi intexrab sylib sseq1 rabbidv
      inteqd wa copab df-span anbi1i opabbii eqtr4i fvopab4g mpdan sylbir ) BEF
      ZBEGZHZBIJBAKZFZALMZNZOZBEPQZVAVERHZVFVAVCALSZVHVAUSVIVGELHUSVITVCUSAELVB
      EBUAUBUCUDVCALUEUFCDBCKZVBFZALMZNZVEUTRIVJBOZVLVDVNVKVCALVJBVBUGUHUIIVJEF
      ZDKVMOZUJZCDUKVJUTHZVPUJZCDUKCDAULVSVQCDVRVOVPVJEPQUMUNUOUPUQUR $.
      $( [2-Jun-2004] $)

    $( Value of supremum of set of subsets of Hilbert space.  Definition of
       supremum in Proposition 1 of [Kalmbach] p. 65. $)
    hsupval2 $p |- ( A C_ ~P ~H ->
                    ( \/H ` A ) = |^| { x e. CH | U. A C_ x } ) $=
      ( vy vz chil cpw wss wcel cuni cv cch crab cint cvv chsup cfv wceq
      ax-hilex pwex elpw2 biimpri c0 wne wa uniss unipw syl6ss helch jctil
      sseq2 elrab sylibr ne0i syl intex sylib unieq sseq1d rabbidv inteqd
      copab dfchsup2 anbi1i opabbii eqtr4i fvopab4g syl11anc ) BEFZGZBVHFZHZBIZ
      AJZGZAKLZMZNHZBOPVPQVKVIBVHERSZTUAVIVOUBUCZVQVIEVOHZVSVIEKHZVLEGZUDVTVIWB
      WAVIVLVHIEBVHUEEUFUGUHUIVNWBAEKVMEVLUJUKULVOEUMUNVOUOUPCDBCJZIZVMGZAKLZMZ
      VPVJNOWCBQZWFVOWHWEVNAKWHWDVLVMWCBUQURUSUTOWCVHGZDJWGQZUDZCDVAWCVJHZWJUDZ
      CDVACDAVBWMWKCDWLWIWJWCVHVRTVCVDVEVFVG $.
      $( [13-Aug-2002] $)

    $( Value of supremum of set of subsets of Hilbert space.  For an alternate
       version of the value, see ~ hsupval2 . $)
    hsupval $p |- ( A C_ ~P ~H -> ( \/H ` A ) = ( _|_ ` ( _|_ ` U. A ) ) ) $=
      ( vx chil cpw wss chsup cfv cuni cv cch crab cint cort hsupval2 wceq
      uniss unipw syl6ss ococin syl eqtr4d ) ACDZEZAFGAHZBIEBJKLZUDMGMGZBANUCUD
      CEUFUEOUCUDUBHCAUBPCQRBUDSTUA $.
      $( [9-Dec-2003] $)

    $( The value of the supremum of a set of closed subspaces of Hilbert
       space.  Definition of supremum in Proposition 1 of [Kalmbach] p. 65. $)
    chsupval2 $p |- ( A C_ CH ->
                    ( \/H ` A ) = |^| { x e. CH | U. A C_ x } ) $=
      ( cch wss chil cpw chsup cfv cuni cv crab cint wceq chsspwh sstr2 mpi
      hsupval2 syl ) BCDZBEFZDZBGHBIAJDACKLMSCTDUANBCTOPABQR $.
      $( [13-Aug-2002] $)

    $( The value of the supremum of a set of closed subspaces of Hilbert
       space.  For an alternate version of the value, see ~ chsupval2 . $)
    chsupval $p |- ( A C_ CH -> ( \/H ` A ) = ( _|_ ` ( _|_ ` U. A ) ) ) $=
      ( cch wss chil cpw chsup cfv cuni cort wceq chsspwh sstr2 mpi hsupval
      syl ) ABCZADEZCZAFGAHIGIGJPBQCRKABQLMANO $.
      $( [13-Aug-2002] $)

    $( The span of a subset of Hilbert space is a subspace. $)
    spancl $p |- ( A C_ ~H -> ( span ` A ) e. SH ) $=
      ( vx chil wss cspn cfv cv csh crab cint spanval c0 wne wa wcel wrex
      helsh sseq2 rcla4ev mpan rabn0 sylibr ssrab2 jctil shintcl syl eqeltrd )
      ACDZAEFABGZDZBHIZJZHBAKUHUKHDZUKLMZNULHOUHUNUMUHUJBHPZUNCHOUHUOQUJUHBCHUI
      CARSTUJBHUAUBUJBHUCUDUKUEUFUG $.
      $( [2-Jun-2004] $)

    $( A member of a span is a vector. $)
    elspancl $p |- ( ( A C_ ~H /\ B e. ( span ` A ) ) -> B e. ~H ) $=
      ( cspn cfv csh wcel chil wss shel spancl sylan ) ACDZEFBLFBGFAGHBLIAJK $.
      $( [17-Dec-2004] $)

    $( Closure of the subspace supremum of set of subsets of Hilbert space. $)
    shsupcl $p |- ( A C_ ~P ~H -> ( span ` U. A ) e. SH ) $=
      ( chil cpw wss cuni cspn cfv csh wcel uniss unipw syl6ss spancl syl ) ABC
      ZDZAEZBDQFGHIPQOEBAOJBKLQMN $.
      $( [26-Nov-2004] $)

    $( Closure of supremum of set of subsets of Hilbert space.  Note that the
       supremum belongs to ` CH ` even if the subsets do not. $)
    hsupcl $p |- ( A C_ ~P ~H -> ( \/H ` A ) e. CH ) $=
      ( vx chil cpw wss chsup cfv cuni cv cch crab cint hsupval2 c0 wne wa
      wcel cvv fvex syl6eqelr intex sylibr ssrab2 jctil chintcl syl eqeltrd )
      ACDEZAFGZAHBIEZBJKZLZJBAMZUHUKJEZUKNOZPULJQUHUOUNUHULRQUOUHULUIRUMAFSTUKU
      AUBUJBJUCUDUKUEUFUG $.
      $( [10-Nov-1999] $)

    $( Closure of supremum of subset of ` CH ` .  Definition of supremum in
       Proposition 1 of [Kalmbach] p. 65.  Shows that ` CH ` is a complete
       lattice.  Also part of Definition 3.4-1 in [MegPav2000] p. 2345 (PDF
       p. 8). $)
    chsupcl $p |- ( A C_ CH -> ( \/H ` A ) e. CH ) $=
      ( cch wss chil cpw chsup cfv wcel chsspwh sstr2 mpi hsupcl syl ) ABCZADEZ
      CZAFGBHNBOCPIABOJKALM $.
      $( [10-Nov-1999] $)

    $( Subset relation for supremum of Hilbert space subsets. $)
    hsupss $p |- ( ( A C_ ~P ~H /\ B C_ ~P ~H ) ->
                  ( A C_ B -> ( \/H ` A ) C_ ( \/H ` B ) ) ) $=
      ( vx chil cpw wss wa chsup cfv cuni cv cch crab cint wceq hsupval2
      adantr adantl sseq12d wi wral wcel uniss sstr2 syl a1d r19.21aiv ss2rab
      sylibr intss syl5bir ) ADEZFZBULFZGZAHIZBHIZFAJZCKZFZCLMZNZBJZUSFZCLMZNZF
      ZABFZUOUPVBUQVFUMUPVBOUNCAPQUNUQVFOUMCBPRSVHVEVAFZVGVHVDUTTZCLUAVIVHVJCLV
      HVJUSLUBVHURVCFVJABUCURVCUSUDUEUFUGVDUTCLUHUIVEVAUJUEUK $.
      $( [24-Nov-2004] $)

    $( Subset relation for supremum of subset of ` CH ` . $)
    chsupss $p |- ( ( A C_ CH /\ B C_ CH ) ->
                  ( A C_ B -> ( \/H ` A ) C_ ( \/H ` B ) ) ) $=
      ( chil cpw wss chsup cfv wi cch hsupss chsspwh sstr2 mpi syl2an ) ACDZEZB
      OEZABEAFGBFGEHAIEZBIEZABJRIOEZPKAIOLMSTQKBIOLMN $.
      $( [13-Aug-2002] $)

    $( A subspace is the supremum of all smaller subspaces. $)
    chsupid $p |- ( A e. CH -> ( \/H ` { x e. CH | x C_ A } ) = A ) $=
      ( vy cch wcel cv wss crab cuni cint chsup cfv unimax sseq1d rabbidv
      inteqd intmin eqtrd wceq ssrab2 chsupval2 ax-mp syl5eq ) BDEZAFBGZADHZIZC
      FZGZCDHZJZBUFKLZUDUKBUHGZCDHZJBUDUJUNUDUIUMCDUDUGBUHABDMNOPCBDQRUFDGULUKS
      UEADTCUFUAUBUC $.
      $( [13-Aug-2002] $)

    $( Value of supremum of subset of ` CH ` on a singleton. $)
    chsupsn $p |- ( A e. CH -> ( \/H ` { A } ) = A ) $=
      ( vx cch wcel csn chsup cfv cuni cv wss crab cint wceq snssi chsupval2
      syl wa unisng eqimss ancli sseq2 elrab sylibr intss1 ssintub sseq1d
      mpbii eqssd eqtrd ) ACDZAEZFGZUKHZBIZJZBCKZLZAUJUKCJULUQMACNBUKOPUJUQAUJA
      UPDZUQAJUJUJUMAJZQURUJUSUJUMAMUSACRZUMASPTUOUSBACUNAUMUAUBUCAUPUDPUJUMUQJ
      AUQJBUMCUEUJUMAUQUTUFUGUHUI $.
      $( [13-Aug-2002] $)

    $( The union of a set of Hilbert space subsets is smaller than its
       supremum. $)
    hsupunss $p |- ( A C_ ~P ~H -> U. A C_ ( \/H ` A ) ) $=
      ( vx chil cpw wss cuni chsup cfv cv cch crab cint ssintub hsupval2
      sseq2d mpbiri ) ACDEZAFZAGHZERRBIEBJKLZEBRJMQSTRBANOP $.
      $( [24-Nov-2004] $)

    $( A subset of Hilbert space is included in its span. $)
    spanss2 $p |- ( A C_ ~H -> A C_ ( span ` A ) ) $=
      ( vx chil wss cspn cfv cv csh crab cint ssintub spanval sseq2d mpbiri )
      ACDZAAEFZDAABGDBHIJZDBAHKOPQABALMN $.
      $( [2-Jun-2004] $)

    $( The union of a set of subspaces is smaller than its supremum. $)
    shsupunss $p |- ( A C_ SH -> U. A C_ ( span ` U. A ) ) $=
      ( csh wss cuni chil cspn cfv cpw shsspwh sstr mpan2 uniss syl unipw
      syl6ss spanss2 ) ABCZADZECRRFGCQREHZDZEQASCZRTCQBSCUAIABSJKASLMENORPM $.
      $( [26-Nov-2004] $)

    $( The union of a set of closed subspaces is smaller than its supremum. $)
    chsupunss $p |- ( A C_ CH -> U. A C_ ( \/H ` A ) ) $=
      ( cch wss chil cpw cuni chsup cfv chsspwh sstr mpan2 hsupunss syl ) ABCZA
      DEZCZAFAGHCNBOCPIABOJKALM $.
      $( [14-Aug-2002] $)

    $( A subspace of Hilbert space is its own span. $)
    spanid $p |- ( A e. SH -> ( span ` A ) = A ) $=
      ( vx csh wcel cspn cfv cv wss crab cint chil wceq shss spanval syl
      intmin eqtrd ) ACDZAEFZABGHBCIJZARAKHSTLAMBANOBACPQ $.
      $( [2-Jun-2004] $)

    $( Ordering relationship for the spans of subsets of Hilbert space. $)
    spanss $p |- ( ( B C_ ~H /\ A C_ B ) -> ( span ` A ) C_ ( span ` B ) ) $=
      ( vx chil wss wa cv csh crab cint cspn cfv wi wral wcel sstr2 a1d
      r19.21aiv ss2rab sylibr intss syl adantl wceq sstr ancoms spanval adantr
      3sstr4d ) BDEZABEZFZACGZEZCHIZJZBUMEZCHIZJZAKLZBKLZUKUPUSEZUJUKURUOEZVBUK
      UQUNMZCHNVCUKVDCHUKVDUMHOABUMPQRUQUNCHSTURUOUAUBUCULADEZUTUPUDUKUJVEABDUE
      UFCAUGUBUJVAUSUDUKCBUGUHUI $.
      $( [2-Jun-2004] $)

    $( The span of a subset of Hilbert space is less than or equal to its
       closure (double orthogonal complement). $)
    spanssoc $p |- ( A C_ ~H -> ( span ` A ) C_ ( _|_ ` ( _|_ ` A ) ) ) $=
      ( chil wss cspn cfv cort ocss syl ococss spanss syl11anc csh wcel wceq
      ocsh spanid 3syl sseqtrd ) ABCZADEZAFEZFEZDEZUBSUBBCZAUBCTUCCSUABCZUDAGZU
      AGHAIAUBJKSUEUBLMUCUBNUFUAOUBPQR $.
      $( [3-Jun-2004] $)

    $( Value of join for subsets of Hilbert space. $)
    sshjval $p |- ( ( A C_ ~H /\ B C_ ~H ) -> ( A vH B ) =
        ( _|_ ` ( _|_ ` ( A u. B ) ) ) ) $=
      ( vx vy vz chil cpw wcel chj co cun cort cfv wceq wss cv fvex uneq1
      fveq2d uneq2 wa copab2 df-chj ax-hilex elpw2 anbi12i anbi1i oprabbii
      eqtr4i oprabval2 syl2anbr ) AFGZHBULHABIJABKZLMZLMZNAFOBFOCDEABULULCPZDPZ
      KZLMZLMZUOIAUQKZLMZLMUNLQUPANZUSVBLVCURVALUPAUQRSSUQBNZVBUNLVDVAUMLUQBATS
      SIUPFOZUQFOZUAZEPUTNZUAZCDEUBUPULHZUQULHZUAZVHUAZCDEUBCDEUCVMVICDEVLVGVHV
      JVEVKVFUPFUDUEUQFUDUEUFUGUHUIUJAFUDUEBFUDUEUK $.
      $( [1-Nov-2000] $)

    $( Value of join in ` SH ` . $)
    shjval $p |- ( ( A e. SH /\ B e. SH ) -> ( A vH B ) =
        ( _|_ ` ( _|_ ` ( A u. B ) ) ) ) $=
      ( chil wss chj co cun cort cfv wceq csh wcel sshjval shss syl2an ) ACDBCD
      ABEFABGHIHIJAKLBKLABMANBNO $.
      $( [9-Aug-2000] $)

    $( Value of join in ` CH ` . $)
    chjval $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH B ) =
        ( _|_ ` ( _|_ ` ( A u. B ) ) ) ) $=
      ( csh wcel chj co cun cort cfv wceq cch shjval chsh syl2an ) ACDBCDABEFAB
      GHIHIJAKDBKDABLAMBMN $.
      $( [9-Aug-2000] $)

    ${
      chjval.1 $e |- A e. CH $.
      chjval.2 $e |- B e. CH $.
      $( Value of join in ` CH ` . $)
      chjvali $p |- ( A vH B ) = ( _|_ ` ( _|_ ` ( A u. B ) ) ) $=
        ( cch wcel chj co cun cort cfv wceq chjval mp2an ) AEFBEFABGHABIJKJKLCD
        ABMN $.
        $( [9-Aug-2000] $)
    $}


    $( Alternate definition of join in the set of closed subspaces of Hilbert
       space ` CH ` . $)
    dfchj2 $p |- vH = { <. <. x , y >. , z >. | ( ( x C_ ~H /\ y C_ ~H ) /\
               z = |^| { w e. CH | ( x u. y ) C_ w } ) } $=
      ( chj cv chil wss wa cun cort cfv wceq copab2 cch crab cint df-chj unss
      ococin sylbi eqeq2d pm5.32i oprabbii eqtri ) EAFZGHBFZGHIZCFZUFUGJZKLKLZM
      ZIZABCNUHUIUJDFHDOPQZMZIZABCNABCRUMUPABCUHULUOUHUKUNUIUHUJGHUKUNMUFUGGSDU
      JTUAUBUCUDUE $.
      $( [1-Nov-2000] $)

    $( Alternate definition of join in the set of closed subspaces of Hilbert
       space ` CH ` : the join is the supremum of its two arguments.  Based on
       the definition of join in [Beran] p. 3.  For later convenience we prove
       a general version that works for any subset of Hilbert space, not just
       the elements of the lattice ` CH ` . $)
    dfchj3 $p |- vH = { <. <. x , y >. , z >. | ( ( x C_ ~H /\ y C_ ~H ) /\
               z = ( \/H ` { x , y } ) ) } $=
      ( chj cv chil wss wa cun cort cfv wceq copab2 cpr chsup df-chj cuni cpw
      visset prsspw hsupval sylbir unipr fveq2i syl6req eqeq2d pm5.32i
      oprabbii eqtri ) DAEZFGBEZFGHZCEZUJUKIZJKZJKZLZHZABCMULUMUJUKNZOKZLZHZABC
      MABCPURVBABCULUQVAULUPUTUMULUTUSQZJKZJKZUPULUSFRGUTVELUJUKFASZBSZTUSUAUBV
      DUOJVCUNJUJUKVFVGUCUDUDUEUFUGUHUI $.
      $( [2-Mar-2004] $)

    $( Value of join for subsets of Hilbert space in terms of supremum: the
       join is the supremum of its two arguments.  Based on the definition of
       join in [Beran] p. 3. $)
    sshjval3 $p |- ( ( A C_ ~H /\ B C_ ~H ) -> ( A vH B ) =
                 ( \/H ` { A , B } ) ) $=
      ( vx vy vz chil cpw wcel chj co cpr chsup cfv wceq wss cv fvex preq1
      fveq2d preq2 wa copab2 dfchj3 ax-hilex elpw2 anbi12i anbi1i oprabbii
      eqtr4i oprabval2 syl2anbr ) AFGZHBULHABIJABKZLMZNAFOBFOCDEABULULCPZDPZKZL
      MZUNIAUPKZLMUMLQUOANUQUSLUOAUPRSUPBNUSUMLUPBATSIUOFOZUPFOZUAZEPURNZUAZCDE
      UBUOULHZUPULHZUAZVCUAZCDEUBCDEUCVHVDCDEVGVBVCVEUTVFVAUOFUDUEUPFUDUEUFUGUH
      UIUJAFUDUEBFUDUEUK $.
      $( [2-Mar-2004] $)

    $( Closure of join for subsets of Hilbert space. $)
    sshjcl $p |- ( ( A C_ ~H /\ B C_ ~H ) -> ( A vH B ) e. CH ) $=
      ( chil wss wa chj co cun cort cfv cch sshjval wcel unss ocss occl syl
      sylbi eqeltrd ) ACDBCDEZABFGABHZIJZIJZKABLTUACDZUCKMZABCNUDUBCDUEUAOUBPQR
      S $.
      $( [1-Nov-2000] $)

    $( Closure of join in ` SH ` . $)
    shjcl $p |- ( ( A e. SH /\ B e. SH ) -> ( A vH B ) e. CH ) $=
      ( chil wss chj co cch wcel csh sshjcl shss syl2an ) ACDBCDABEFGHAIHBIHABJ
      AKBKL $.
      $( [2-Nov-1999] $)

    $( Closure of join in ` CH ` . $)
    chjcl $p |- ( ( A e. CH /\ B e. CH ) -> ( A vH B ) e. CH ) $=
      ( csh wcel chj co cch shjcl chsh syl2an ) ACDBCDABEFGDAGDBGDABHAIBIJ $.
      $( [2-Nov-1999] $)

    $( Commutative law for Hilbert lattice join of subspaces. $)
    shjcom $p |- ( ( A e. SH /\ B e. SH ) -> ( A vH B ) = ( B vH A ) ) $=
      ( csh wcel wa chj co cun cort cfv shjval wceq ancoms uncom fveq2i syl6eq
      eqtr4d ) ACDZBCDZEZABFGABHZIJZIJZBAFGZABKTUDBAHZIJZIJZUCSRUDUGLBAKMUFUBIU
      EUAIBANOOPQ $.
      $( [22-Jun-2004] $)

    ${
      $d x y D $.
      shincl.1 $e |- A e. SH $.
      shincl.2 $e |- B e. SH $.
      $( Closure of intersection of two subspaces. $)
      shincli $p |- ( A i^i B ) e. SH $=
        ( cpr cint cin csh elisseti intpr wss c0 wne wcel wa pm3.2i prss mpbi
        prnz shintcli eqeltrri ) ABEZFABGHABAHCIZBHDIZJUBUBHKZUBLMAHNZBHNZOUEUF
        UGCDPABHUCUDQRABUCSPTUA $.
        $( [19-Oct-1999] $)

      $( Commutative law for subspace sum. $)
      shscomi $p |- ( A +H B ) = ( B +H A ) $=
        ( csh wcel cph co wceq shscom mp2an ) AEFBEFABGHBAGHICDABJK $.
        $( [17-Oct-1999] $)

      $( Vector sum belongs to subspace sum. $)
      shsvai $p |- ( ( C e. A /\ D e. B ) -> ( C +h D ) e. ( A +H B ) ) $=
        ( csh wcel wa cva co cph wi shsva mp2an ) AGHBGHCAHDBHICDJKABLKHMEFABCD
        NO $.
        $( [17-Oct-1999] $)

      $( A subspace sum contains a member of one of its subspaces. $)
      shsel1i $p |- ( C e. A -> C e. ( A +H B ) ) $=
        ( csh wcel cph co wi shsel1 mp2an ) AFGBFGCAGCABHIGJDEABCKL $.
        $( [19-Oct-1999] $)

      $( A subspace sum contains a member of one of its subspaces. $)
      shsel2i $p |- ( C e. B -> C e. ( A +H B ) ) $=
        ( csh wcel cph co wi shsel2 mp2an ) AFGBFGCBGCABHIGJDEABCKL $.
        $( [19-Oct-1999] $)

      $( Vector subtraction belongs to subspace sum. $)
      shsvsi $p |- ( ( C e. A /\ D e. B ) -> ( C -h D ) e. ( A +H B ) ) $=
        ( cph co wcel cmv csh wa wi shscli shsubclOLD ax-mp shsel1i shsel2i
        syl2an ) CABGHZIZDTIZCDJHTIZCAIDBITKIUAUBLUCMABEFNCDTOPABCEFQABDEFRS $.
        $( [19-Oct-1999] $)

      $( Union is smaller than subspace sum. $)
      shunssi $p |- ( A u. B ) C_ ( A +H B ) $=
        ( vx vy vz cun cph co cv wcel wo cva wceq wrex c0v chil sheli
        ax-hvaddid eqcomd syl csh sh0 ax-mp rcla4eopr mp3an2 mpdan hvaddid2
        mp3an1 jaoi elun shseli 3imtr4i ssriv ) EABHZABIJZEKZALZURBLZMURFKGKNJO
        GBPFAPZURUPLURUQLUSVAUTUSURURQNJZOZVAUSURRLZVCURACSVDVBURURTUAUBUSQBLZV
        CVABUCLVEDBUDUEFGABURQURNUFUGUHUTURQURNJZOZVAUTVDVGURBDSVDVFURURUIUAUBQ
        ALZUTVGVAAUCLVHCAUDUEFGABQURURNUFUJUHUKURABULFGABURCDUMUNUO $.
        $( [18-Oct-1999] $)

      $( Subspace sum is smaller than Hilbert lattice join.  Remark in
         [Kalmbach] p. 65. $)
      shsleji $p |- ( A +H B ) C_ ( A vH B ) $=
        ( vx vy vz cph co chj cv cva wceq wrex cun cort cfv wcel wa eleq1 csh
        wi chil wss shssii unssi ocss ax-mp ocsh shaddclOLD elun1 ococss sseli
        syl elun2 syl2an syl5cbir r19.23aivv shseli shjval mp2an eleq2i
        3imtr4i ssriv ) EABHIZABJIZEKZFKZGKZLIZMZGBNFANVGABOZPQZPQZRZVGVERVGVFR
        VKVOFGABVKVOVJVNRZVHARZVIBRZSVGVJVNTVHVNRZVIVNRZVPVQVRVNUARZVSVTSVPUBVM
        UCUDZWAVLUCUDZWBABUCACUEBDUEUFZVLUGUHVMUIUHVHVIVNUJUHVQVHVLRVSVHABUKVLV
        NVHWCVLVNUDWDVLULUHZUMUNVRVIVLRVTVIBAUOVLVNVIWEUMUNUPUQURFGABVGCDUSVFVN
        VGAUARBUARVFVNMCDABUTVAVBVCVD $.
        $( [19-Oct-1999] $)

      $( Union is smaller than Hilbert lattice join. $)
      shunssji $p |- ( A u. B ) C_ ( A vH B ) $=
        ( cun cph co chj shunssi shsleji sstri ) ABEABFGABHGABCDIABCDJK $.
        $( [11-Jun-2004] $)

      $( Commutative law for join in ` SH ` . $)
      shjcomi $p |- ( A vH B ) = ( B vH A ) $=
        ( csh wcel chj co wceq shjcom mp2an ) AEFBEFABGHBAGHICDABJK $.
        $( [19-Oct-1999] $)

      $( Subspace sum is an upper bound of its arguments. $)
      shsub1i $p |- A C_ ( A +H B ) $=
        ( vx cph co cv shsel1i ssriv ) EAABFGABEHCDIJ $.
        $( [19-Oct-1999] $)

      $( Subspace sum is an upper bound of its arguments. $)
      shsub2i $p |- A C_ ( B +H A ) $=
        ( vx cph co cv shsel2i ssriv ) EABAFGBAEHDCIJ $.
        $( [17-Dec-2004] $)

      $( Hilbert lattice join is an upper bound of two subspaces. $)
      shub1i $p |- A C_ ( A vH B ) $=
        ( cph co chj shsub1i shsleji sstri ) AABEFABGFABCDHABCDIJ $.
        $( [19-Oct-1999] $)

      $( Closure of ` CH ` join. $)
      shjcli $p |- ( A vH B ) e. CH $=
        ( csh wcel chj co cch shjcl mp2an ) AEFBEFABGHIFCDABJK $.
        $( [19-Oct-1999] $)

      $( ` SH ` closure of join. $)
      shjshcli $p |- ( A vH B ) e. SH $=
        ( chj co shjcli chshii ) ABEFABCDGH $.
        $( [5-May-2000] $)

      ${
        $d x y C $.
        shlub.1 $e |- C e. CH $.
        $( Hilbert lattice join is the least upper bound (among Hilbert lattice
           elements) of two subspaces. $)
        shlubi $p |- ( ( A C_ C /\ B C_ C ) <-> ( A vH B ) C_ C ) $=
          ( wss wa chj co cun cort cfv chil shssii unssi chssii occon2i unss
          csh wcel wceq shjval mp2an pjococi eqcomi sseq12i 3imtr4i shub1i
          sstr mpan ssun2 shunssji sstri jca impbii ) ACGZBCGZHZABIJZCGZABKZCGV
          BLMLMZCLMLMZGUSVAVBCABNADOBEOPCFQRABCSUTVCCVDATUABTUAUTVCUBDEABUCUDVD
          CCFUEUFUGUHVAUQURAUTGVAUQABDEUIAUTCUJUKBUTGVAURBVBUTBAULABDEUMUNBUTCU
          JUKUOUP $.
          $( [11-Jun-2004] $)
      $}

      ${
        shless.1 $e |- C e. SH $.
        $( Subset implies subset of subspace sum. $)
        shlessi $p |- ( A C_ B -> ( A +H C ) C_ ( B +H C ) ) $=
          ( vx vy vz wss cph co cv cva wceq wrex wcel ssel anim1d reximdv2
          shseli 3imtr4g ssrdv ) ABJZGACKLZBCKLZUDGMZHMZIMNLOICPZHAPUIHBPUGUEQU
          GUFQUDUIUIHABUDUHAQUHBQUIABUHRSTHIACUGDFUAHIBCUGEFUAUBUC $.
          $( [18-Nov-2000] $)

        $( Add disjunct to both sides of Hilbert subspace ordering. $)
        shlej1i $p |- ( A C_ B -> ( A vH C ) C_ ( B vH C ) ) $=
          ( wss chj co wa shub1i sstr2 mpi shjcomi sseqtri jctir shjcli shlubi
          sylib ) ABGZABCHIZGZCUAGZJACHIUAGTUBUCTBUAGUBBCEFKABUALMCCBHIUACBFEKC
          BFENOPACUADFBCEFQRS $.
          $( [19-Oct-1999] $)

        $( Add disjunct to both sides of Hilbert subspace ordering. $)
        shlej2i $p |- ( A C_ B -> ( C vH A ) C_ ( C vH B ) ) $=
          ( wss chj co shlej1i shjcomi 3sstr4g ) ABGACHIBCHICAHICBHIABCDEFJCAFD
          KCBFEKL $.
          $( [19-Oct-1999] $)
      $}
    $}
  $}

  $( Subspace sum is smaller than subspace join.  Remark in [Kalmbach]
     p. 65. $)
  shslej $p |- ( ( A e. SH /\ B e. SH ) -> ( A +H B ) C_ ( A vH B ) ) $=
    ( csh wcel cph co chj wss chil cif wceq opreq1 sseq12d opreq2 helsh elimel
    shsleji dedth2h ) ACDZBCDZABEFZABGFZHSAIJZBEFZUCBGFZHUCTBIJZEFZUCUFGFZHABII
    AUCKUAUDUBUEAUCBELAUCBGLMBUFKUDUGUEUHBUFUCENBUFUCGNMUCUFAICOPBICOPQR $.
    $( [12-Jul-2004] $)

  $( Closure of intersection of two subspaces. $)
  shincl $p |- ( ( A e. SH /\ B e. SH ) -> ( A i^i B ) e. SH ) $=
    ( csh wcel cin chil cif wceq ineq1 eleq1d ineq2 helsh elimel shincli
    dedth2h ) ACDZBCDZABEZCDPAFGZBEZCDSQBFGZEZCDABFFASHRTCASBIJBUAHTUBCBUASKJSU
    AAFCLMBFCLMNO $.
    $( [24-Jun-2004] $)

  $( Hilbert lattice join is an upper bound of two subspaces. $)
  shub1 $p |- ( ( A e. SH /\ B e. SH ) -> A C_ ( A vH B ) ) $=
    ( csh wcel wa cph co chj shsub1 shslej sstrd ) ACDBCDEAABFGABHGABIABJK $.
    $( [22-Jun-2004] $)

  $( A subspace is a subset of its Hilbert lattice join with another. $)
  shub2 $p |- ( ( A e. SH /\ B e. SH ) -> A C_ ( B vH A ) ) $=
    ( csh wcel wa chj co shub1 shjcom sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
    $( [22-Jun-2004] $)

  $( Hilbert lattice join is the least upper bound (among Hilbert lattice
     elements) of two subspaces. $)
  shlub $p |- ( ( A e. SH /\ B e. SH /\ C e. CH ) ->
               ( ( A C_ C /\ B C_ C ) <-> ( A vH B ) C_ C ) ) $=
    ( csh wcel cch wss wa chj co wb chil cif wceq sseq1 anbi1d opreq1 sseq1d
    bibi12d anbi2d opreq2 sseq2 anbi12d helsh elimel helch shlubi dedth3h ) ADE
    ZBDEZCFEZACGZBCGZHZABIJZCGZKUIALMZCGZUMHZUQBIJZCGZKURUJBLMZCGZHZUQVBIJZCGZK
    UQUKCLMZGZVBVGGZHZVEVGGZKABCLLLAUQNZUNUSUPVAVLULURUMAUQCOPVLUOUTCAUQBIQRSBV
    BNZUSVDVAVFVMUMVCURBVBCOTVMUTVECBVBUQIUARSCVGNZVDVJVFVKVNURVHVCVICVGUQUBCVG
    VBUBUCCVGVEUBSUQVBVGALDUDUEBLDUDUECLFUFUEUGUH $.
    $( [15-Jun-2004] $)

  $( Add disjunct to both sides of Hilbert subspace ordering. $)
  shlej1 $p |- ( ( ( A e. SH /\ B e. SH /\ C e. SH ) /\
               A C_ B ) -> ( A vH C ) C_ ( B vH C ) ) $=
    ( csh wcel w3a wss chj co wa wi sstr2 shub1 syl5com shub2 ancoms jctird
    3adant1 cch wb simp1 simp3 shjcl shlub syl111anc sylibd imp ) ADEZBDEZCDEZF
    ZABGZACHIBCHIZGZUKULAUMGZCUMGZJZUNUIUJULUQKUHUIUJJZULUOUPULBUMGUOURABUMLBCM
    NUJUIUPCBOPQRUKUHUJUMSEZUQUNTUHUIUJUAUHUIUJUBUIUJUSUHBCUCRACUMUDUEUFUG $.
    $( [27-Dec-2006] $) $( [22-Jun-2004] $)

  $( Add disjunct to both sides of Hilbert subspace ordering. $)
  shlej2 $p |- ( ( ( A e. SH /\ B e. SH /\ C e. SH ) /\
               A C_ B ) -> ( C vH A ) C_ ( C vH B ) ) $=
    ( csh wcel w3a wss wa chj co shlej1 wceq shjcom 3adant2 adantr 3adant1
    3sstr3d ) ADEZBDEZCDEZFZABGZHACIJZBCIJZCAIJZCBIJZABCKUAUCUELZUBRTUGSACMNOUA
    UDUFLZUBSTUHRBCMPOQ $.
    $( [3-Jan-2007] $) $( [22-Jun-2004] $)

  ${
    $d x y z A $.
    shsidm.1 $e |- A e. SH $.
    $( Idempotent law for Hilbert subspace sum. $)
    shsidmi $p |- ( A +H A ) = A $=
      ( vx vy vz cph co cv wcel cva wceq wrex shseli wa eleq1 csh wi
      shaddclOLD ax-mp syl5cbir r19.23aivv sylbi ssriv shsub1i eqssi ) AAFGZACU
      FACHZUFIUGDHZEHZJGZKZEALDALUGAIZDEAAUGBBMUKULDEAAUKULUJAIZUHAIUIAINZUGUJA
      OAPIUNUMQBUHUIARSTUAUBUCAABBUDUE $.
      $( [6-Jun-2004] $)
  $}

  ${
    shslub.1 $e |- A e. SH $.
    shslub.2 $e |- B e. SH $.
    shslub.3 $e |- C e. SH $.
    $( Least upper bound law for Hilbert subspace sum. $)
    shslubi $p |- ( ( A C_ C /\ B C_ C ) <-> ( A +H B ) C_ C ) $=
      ( wss wa cph co shlessi shscomi syl6ss shsidmi sylan9ss shsub1i sstr
      mpan shsub2i jca impbii ) ACGZBCGZHABIJZCGZUBUCUDBCIJZCUBUDCBIJUFACBDFEKC
      BFELMUCUFCCIJCBCCEFFKCFNMOUEUBUCAUDGUEUBABDEPAUDCQRBUDGUEUCBAEDSBUDCQRTUA
      $.
      $( [15-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    shlesb1.1 $e |- A e. SH $.
    shlesb1.2 $e |- B e. SH $.
    $( Hilbert lattice ordering in terms of subspace sum. $)
    shlesb1i $p |- ( A C_ B <-> ( A +H B ) = B ) $=
      ( wss wa cph co wceq ssid biantrur shslubi eqss shsub2i mpbiran2 shscomi
      sseq1i bitr2i 3bitri ) ABEZBBEZTFBAGHZBEZABGHZBIZUATBJKBABDCDLUEUDBEZUCUE
      UFBUDEUDBMBADCNOUDUBBABCDPQRS $.
      $( [23-Nov-2004] $)

    $( An alternate way to express subspace sum. $)
    shsumval2i $p |- ( A +H B ) = |^| { x e. SH | ( A u. B ) C_ x } $=
      ( cph co cun cv wss csh crab cint wa ssun1 ssintub sstri ssun2 pm3.2i c0
      wne wcel ssrab2 wrex shscli shunssi sseq2 rcla4ev mp2an rabn0 mpbir
      shintcl shslubi mpbi elrab mpbir2an intss1 ax-mp eqssi ) BCFGZBCHZAIZJZAK
      LZMZBVEJZCVEJZNUTVEJVFVGBVAVEBCOAVAKPZQCVAVECBRVHQSBCVEDEVDKJVDTUAZVEKUBV
      CAKUCVIVCAKUDZUTKUBZVAUTJZVJBCDEUEZBCDEUFZVCVLAUTKVBUTVAUGZUHUIVCAKUJUKVD
      ULUIUMUNUTVDUBZVEUTJVPVKVLVCVLAUTKVOUOVMVNUPUTVDUQURUS $.
      $( [25-Nov-2004] $)

    $( An alternate way to express subspace sum. $)
    shsumval3i $p |- ( A +H B ) = ( span ` ( A u. B ) ) $=
      ( vx cph co cun cv wss csh crab cint cspn cfv shsumval2i chil wceq
      shssii unssi spanval ax-mp eqtr4i ) ABFGABHZEIJEKLMZUDNOZEABCDPUDQJUFUERA
      BQACSBDSTEUDUAUBUC $.
      $( [25-Nov-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    shmod.1 $e |- A e. SH $.
    shmod.2 $e |- B e. SH $.
    shmod.3 $e |- C e. SH $.
    $( The modular law holds for subspace sum.  Similar to part of Theorem 16.9
       of [MaedaMaeda] p. 70. $)
    shmodsi $p |- ( A C_ C -> ( ( A +H B ) i^i C ) C_ ( A +H ( B i^i C ) ) ) $=
      ( vz vx vy wss cph co cin cv wcel wa cva wceq wrex wi cmv wb w3a chil
      hvsubadd sheli syl3an eqcom syl6bb 3expb shlesb1i biimpi eleq2d shsvsi
      shscomi syl6eleq syl5bi eleq1 biimpd sylan9 anim2d elin syl6ibr ex com13
      ancoms anasss sylbird imp shincli shsvai syl5bir exp3a com12 ad2antrl
      syld exp31 r19.23advv shseli syl5ib imp3a ssrdv ) ACJZGABKLZCMZABCMZKLZWC
      GNZWDOZWHCOZPWHWGOZWHWEOWCWIWJWKWJWIWCWKWJWHHNZINZQLZRZIBSHASWCWKTZWIWJWO
      WPHIABWJWLAOZWMBOZPZWOWPWJWSPZWOPWCWMWFOZWKWTWOWCXATZWTWOWHWLUALZWMRZXBWJ
      WQWRXDWOUBWJWQWRUCXDWNWHRZWOWHUDOWLUDOWMUDOXDXEUBWJWQWRWHWLWMUEWHCFUFWLAD
      UFWMBEUFUGWNWHUHUIUJWJWQWRXDXBTZWRWJWQPZXFWCXDWRXGPZXAWCXDXHXATWCXDPZXHWR
      WMCOZPXAXIXGXJWRWCXGXCCOZXDXJWCXCACKLZOXKXGWCXLCXCWCXLCRACDFUKULUMXGXCCAK
      LXLCAWHWLFDUNCAFDUOUPUQXDXKXJXCWMCURUSUTVAWMBCVBVCVDVEVFVGVHVIWTWOXAWKTZW
      QWOXMTWJWRWOWQXMWOWQXAWKWOWKWNWGOWQXAPWHWNWGURAWFWLWMDBCEFVJVKVLVMVNVOVIV
      PVQVRHIABWHDEVSVTVEWAWHWDCVBVTWB $.
      $( [23-Nov-2004] $)

    $( The modular law is implied by the closure of subspace sum.  Part of
       proof of Theorem 16.9 of [MaedaMaeda] p. 70. $)
    shmodi $p |- ( ( ( A +H B ) = ( A vH B ) /\ A C_ C ) ->
                ( ( A vH B ) i^i C ) C_ ( A vH ( B i^i C ) ) ) $=
      ( cph co chj wceq wss wa cin ineq1 sseq1d shmodsi syl5bi imp shincli
      shsleji sstr mpan2 syl ) ABGHZABIHZJZACKZLUECMZABCMZGHZKZUHAUIIHZKZUFUGUK
      UFUDCMZUJKUKUGUFUNUHUJUDUECNOABCDEFPQRUKUJULKUMAUIDBCEFSTUHUJULUAUBUC $.
      $( [23-Nov-2004] $)
  $}

