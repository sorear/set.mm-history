$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Superposition principle
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d x y z B $.  $d v w x y z $.
    $( Superposition Principle.  If ` A ` and ` B ` are distinct atoms, there
       exists a third atom, distinct from ` A ` and ` B ` , that is the
       superposition of ` A ` and ` B ` .  Definition 3.4-3(a) in [MegPav2000]
       p. 2345 (PDF p. 8). $)
    superpos $p |- ( ( A e. HAtoms /\ B e. HAtoms /\ A =/= B ) ->
                   E. x e. HAtoms ( x =/= A /\ x =/= B /\ x C_ ( A vH B ) ) )
        $=
      ( vy vz vw vv wcel wne co wrex cspn wceq wa chil wi wb adantr csm cc cneg
      cat chj wss w3a c0v csn cfv atom1d reeanv an4 neeq1 neeq2 sylan9bb adantl
      cv cva hvaddcl c1 hvaddeq0 fveq2d ax-1cn negcli ax-1ne0 negne0i spansncol
      cc0 mp3an23 sylan9eqr ex sylbid necon3d imp spansna syl2anc adantlr eqeq2
      biimpd spansneleqi syl elspansn caddc addcl mpan2 ad2antlr hvmulcl ancoms
      sneq simpll simplr hvsubadd syl3anc biimpar hvsubval sylancom ax-hvdistr2
      mp3an2 eqtr4d eqtr3d opreq1 eqeq2d rcla4ev exp31 rexlimdv syld spansneleq
      cmv sylibrd eqcom syl6ib sylan9r adantlrl adantrr adantll ax-hvcom eqeq1d
      bitr4d adantlrr adantrl cpr spanpr opreq12 cph df-pr fveq2i spanun syl2an
      cun snssi syl5eq spansnch spansnj sylan sseqtr4d sseq1 3anbi123d syl13anc
      cch eqtr2d expl syl5bi rexlimivv sylbir syl2anb 3impia ) BUBHZCUBHZBCIZAU
      PZBIZUUICIZUUIBCUCJZUDZUEZAUBKZUUFDUPZUFIZBUUPUGZLUHZMZNZDOKZEUPZUFIZCUVC
      UGZLUHZMZNZEOKZUUHUUOPZUUGDBUIECUIUVBUVINUVAUVHNZEOKDOKUVJUVAUVHDEOOUJUVK
      UVJDEOOUVKUUQUVDNZUUTUVGNZNUUPOHZUVCOHZNZUVJUUQUUTUVDUVGUKUVPUVLUVMUVJUVP
      UVLNZUVMNZUUHUUSUVFIZUUOUVMUUHUVSQUVQUUTUUHUUSCIUVGUVSBUUSCULCUVFUUSUMUNU
      OUVRUVSUUOUVRUVSNUUPUVCUQJZUGLUHZUBHZUWABIZUWACIZUWAUULUDZUUOUVQUVSUWBUVM
      UVPUVSUWBUVLUVPUVSNUVTOHZUVTUFIZUWBUVPUWFUVSUUPUVCURZRUVPUVSUWGUVPUVTUFUU
      SUVFUVPUVTUFMUUPUSUAZUVCSJZMZUUSUVFMZUUPUVCUTUVOUWKUWLPUVNUVOUWKUWLUWKUVO
      UUSUWJUGZLUHZUVFUWKUURUWMLUUPUWJWHVAUVOUWITHZUWIVGIUWNUVFMUSVBVCZUSVBVDVE
      UVCUWIVFVHVIVJUOVKVLVMUVTVNVOVPVPUVRUVSUWCUVQUUTUVSUWCPZUVGUVPUVDUUTUWQUU
      QUVPUVDNZUUTNUWABUUSUVFUUTUWABMZUWAUUSMZUWRUWLUUTUWSUWTBUUSUWAVQVRUWRUWTU
      VCUUSHZUWLUVPUWTUXAPUVDUVPUWTUVCFUPZUUPSJZMZFTKZUXAUVPUWTUVTUUSHZUXEUVPUW
      FUWTUXFPUWHUVTUUPVSVTUVPUXFUVTGUPZUUPSJZMZGTKZUXEUVNUXFUXJQUVOGUUPUVTWARU
      VPUXIUXEGTUVPUXGTHZUXIUXEUVPUXKNZUXINZUXGUWIWBJZTHZUVCUXNUUPSJZMZUXEUXKUX
      OUVPUXIUXKUWOUXOUWPUXGUWIWCWDZWEUXMUXHUUPXGJZUVCUXPUXLUXSUVCMZUXIUXLUXHOH
      ZUVNUVOUXTUXIQUVNUXKUYAUVOUXKUVNUYAUXGUUPWFZWGVPUVNUVOUXKWIZUVNUVOUXKWJZU
      XHUUPUVCWKWLWMUXLUXSUXPMZUXIUVNUXKUYEUVOUXKUVNUYEUXKUVNNUXSUXHUWIUUPSJUQJ
      ZUXPUXKUVNUYAUXSUYFMUYBUXHUUPWNWOUXKUWOUVNUXPUYFMUWPUXGUWIUUPWPWQWRWGVPRW
      SUXDUXQFUXNTUXBUXNMZUXCUXPUVCUXBUXNUUPSWTXAXBVOXCXDVKXEUVNUXAUXEQUVOFUUPU
      VCWARXHRUVNUVDUXAUWLPUVOUVNUVDNUXAUVFUUSMUWLUVCUUPXFUVFUUSXIXJVPXEXKVLXLX
      MVMUVRUVSUWDUVQUVGUVSUWDPZUUTUVPUUQUVGUYHUVDUVPUUQNZUVGNUWACUUSUVFUVGUWAC
      MZUWAUVFMZUYIUWLUVGUYJUYKCUVFUWAVQVRUYIUYKUUPUVFHZUWLUVPUYKUYLPUUQUVPUYKU
      UPUXBUVCSJZMZFTKZUYLUVPUYKUVTUVFHZUYOUVPUWFUYKUYPPUWHUVTUVCVSVTUVPUYPUVTU
      XGUVCSJZMZGTKZUYOUVOUYPUYSQUVNGUVCUVTWAUOUVPUYRUYOGTUVPUXKUYRUYOUXLUYRNZU
      XOUUPUXNUVCSJZMZUYOUXKUXOUVPUYRUXRWEUYTUYQUVCXGJZUUPVUAUXLVUCUUPMZUYRUXLV
      UDUVCUUPUQJZUYQMZUYRUXLUYQOHZUVOUVNVUDVUFQUVOUXKVUGUVNUXKUVOVUGUXGUVCWFZW
      GXNUYDUYCUYQUVCUUPWKWLUXLUVTVUEUYQUVPUVTVUEMUXKUUPUVCXORXPXQWMUXLVUCVUAMZ
      UYRUVOUXKVUIUVNUXKUVOVUIUXKUVONVUCUYQUWJUQJZVUAUXKUVOVUGVUCVUJMVUHUYQUVCW
      NWOUXKUWOUVOVUAVUJMUWPUXGUWIUVCWPWQWRWGXNRWSUYNVUBFUXNTUYGUYMVUAUUPUXBUXN
      UVCSWTXAXBVOXCXDVKXEUVOUYLUYOQUVNFUVCUUPWAUOXHRUVOUUQUYLUWLPUVNUUPUVCXFXN
      XEXKVLXRXSVMUVRUWEUVSUVPUVMUWEUVLUVPUVMNUWAUUPUVCXTZLUHZUULUVPUWAVULUDUVM
      UUPUVCYARUVMUVPUULUUSUVFUCJZVULBUUSCUVFUCYBUVPVULUUSUVFYCJZVUMUVPVULUURUV
      EYHZLUHZVUNVUKVUOLUUPUVCYDYEUVNUUROUDUVEOUDVUPVUNMUVOUUPOYIUVCOYIUURUVEYF
      YGYJUVNUUSYRHUVOVUNVUMMUUPYKUUSUVCYLYMYSVIYNVPRUUNUWCUWDUWEUEAUWAUBUUIUWA
      MUUJUWCUUKUWDUUMUWEUUIUWABULUUIUWACULUUIUWAUULYOYPXBYQVJVKYTUUAUUBUUCUUDU
      UE $.
      $( [9-Jun-2006] $)
  $}

