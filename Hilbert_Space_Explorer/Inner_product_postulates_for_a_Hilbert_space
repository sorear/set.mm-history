$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Inner product postulates for a Hilbert space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Closure of inner product. $)
  ax-hicl $a |- ( ( A e. H~ /\ B e. H~ ) -> ( A .ih B ) e. CC ) $.

  ${
    hicl.1 $e |- A e. H~ $.
    hicl.2 $e |- B e. H~ $.
    $( Closure inference for inner product. $)
    hicl $p |- ( A .ih B ) e. CC $=
      ( chil wcel csp co cc ax-hicl mp2an ) AEFBEFABGHIFCDABJK $.
      $( [1-Aug-1999] $)
  $}

  $( Conjugate law for inner product.  Postulate (S1) of [Beran] p. 95.
     Note that ` * `` x ` is the complex conjugate ~ cjvalt of ` x ` .
     In the literature, the inner product of ` A ` and ` B ` is usually
     written ` <. A , B >. ` and called Dirac bra-ket notation, but our
     operation notation ~ co allows us to use existing theorems about
     operations and also avoids a clash with the definition of an ordered pair
     ~ df-op . $)
  ax-his1 $a |- ( ( A e. H~ /\ B e. H~ ) ->
                ( A .ih B ) = ( * ` ( B .ih A ) ) ) $.

  $( Distributive law for inner product.  Postulate (S2) of [Beran] p. 95. $)
  ax-his2 $a |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                ( ( A +h B ) .ih C ) = ( ( A .ih C ) + ( B .ih C ) ) ) $.

  $( Associative law for inner product.  Postulate (S3) of [Beran] p. 95.
     Warning:  Mathematics textbooks usually use our version of the axiom.
     Physics textbooks, on the other hand, usually replace the left-hand side
     with ` ( B .ih ( A .h C ) ) ` (e.g. Equation 1.21b of [Hughes] p. 44;
     Definition (iii) of [ReedSimon] p. 36).  See the comments in ~ df-bra
     for why the physics definition is swapped. $)
  ax-his3 $a |- ( ( A e. CC /\ B e. H~ /\ C e. H~ ) ->
                ( ( A .h B ) .ih C ) = ( A x. ( B .ih C ) ) ) $.

  $( Identity law for inner product.  Postulate (S4) of [Beran] p. 95. $)
  ax-his4 $a |- ( ( A e. H~ /\  A =/= 0h ) -> 0 < ( A .ih A ) ) $.

  $( Identity law for inner product.  Postulate (S4) of [Beran] p. 95. $)
  ax-his4OLD $p |- ( ( A e. H~ /\ -. A = 0h ) -> 0 < ( A .ih A ) ) $=
    ( chil wcel c0v wne cc0 csp co clt wbr wceq wn ax-his4 df-ne sylan2br ) ABC
    ADEFAAGHIJADKLAMADNO $.
    $( [6-Sep-1999] $)

