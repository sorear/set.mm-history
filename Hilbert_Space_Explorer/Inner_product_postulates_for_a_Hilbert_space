$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Inner product postulates for a Hilbert space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Inner product maps pairs from ` H~ ` to ` CC ` . $)
  ax-hfi $a |- .ih : ( H~ X. H~ ) --> CC $.

  $( Closure of inner product. $)
  hicl $p |- ( ( A e. H~ /\ B e. H~ ) -> ( A .ih B ) e. CC ) $=
    ( cc chil csp ax-hfi foprcl ) ABCDDEFG $.
    $( [20-Nov-2007] $) $( [17-Nov-2007] $)

  ${
    hicl.1 $e |- A e. H~ $.
    hicl.2 $e |- B e. H~ $.
    $( Closure inference for inner product. $)
    hicli $p |- ( A .ih B ) e. CC $=
      ( chil wcel csp co cc hicl mp2an ) AEFBEFABGHIFCDABJK $.
      $( [1-Aug-1999] $)
  $}

  $( Conjugate law for inner product.  Postulate (S1) of [Beran] p. 95.
     Note that ` * `` x ` is the complex conjugate ~ cjval of ` x ` .
     In the literature, the inner product of ` A ` and ` B ` is usually
     written ` <. A , B >. ` , but our operation notation ~ co allows us to use
     existing theorems about operations and also avoids a clash with the
     definition of an ordered pair ~ df-op .  Physicists use ` <. B | A >. ` ,
     called Dirac bra-ket notation, to represent this operation; see comments
     in ~ df-bra . $)
  ax-his1 $a |- ( ( A e. H~ /\ B e. H~ ) ->
                ( A .ih B ) = ( * ` ( B .ih A ) ) ) $.

  $( Distributive law for inner product.  Postulate (S2) of [Beran] p. 95. $)
  ax-his2 $a |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                ( ( A +h B ) .ih C ) = ( ( A .ih C ) + ( B .ih C ) ) ) $.

  $( Associative law for inner product.  Postulate (S3) of [Beran] p. 95.
     Warning:  Mathematics textbooks usually use our version of the axiom.
     Physics textbooks, on the other hand, usually replace the left-hand side
     with ` ( B .ih ( A .h C ) ) ` (e.g. Equation 1.21b of [Hughes] p. 44;
     Definition (iii) of [ReedSimon] p. 36).  See the comments in ~ df-bra
     for why the physics definition is swapped. $)
  ax-his3 $a |- ( ( A e. CC /\ B e. H~ /\ C e. H~ ) ->
                ( ( A .h B ) .ih C ) = ( A x. ( B .ih C ) ) ) $.

  $( Identity law for inner product.  Postulate (S4) of [Beran] p. 95. $)
  ax-his4 $a |- ( ( A e. H~ /\  A =/= 0h ) -> 0 < ( A .ih A ) ) $.

