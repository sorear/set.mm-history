$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Inner product
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Associative law for inner product.  Lemma 3.1(S5) of [Beran] p. 95. $)
  his5 $p |- ( ( A e. CC /\ B e. ~H /\ C e. ~H ) ->
                ( B .ih ( A .h C ) ) = ( ( * ` A ) x. ( B .ih C ) ) ) $=
    ( cc wcel chil w3a csm co csp ccj cfv cmul wceq wa hvmulcl ax-his1 sylan2
    3impb 3com12 ax-his3 3com23 fveq2d hicl cjmul 3adant1 opreq2d eqtr4d
    3eqtrd ) ADEZBFEZCFEZGZBACHIZJIZUNBJIZKLZACBJIZMIZKLZAKLZBCJIZMIZUKUJULUOUQ
    NZUKUJULVDUJULOUKUNFEVDACPBUNQRSTUMUPUSKUJULUKUPUSNACBUAUBUCUMUTVAURKLZMIZV
    CUJULUKUTVFNZUJULUKVGULUKOUJURDEVGCBUDAURUERSUBUMVBVEVAMUKULVBVENUJBCQUFUGU
    HUI $.
    $( [29-Jul-1999] $)

  $( Associative law for inner product. $)
  his52 $p |- ( ( A e. CC /\ B e. ~H /\ C e. ~H ) ->
                ( B .ih ( ( * ` A ) .h C ) ) = ( A x. ( B .ih C ) ) ) $=
    ( cc wcel chil w3a ccj cfv csm co csp cmul wceq cjcl his5 syl3an1 cjcj
    opreq1d 3ad2ant1 eqtrd ) ADEZBFEZCFEZGBAHIZCJKLKZUEHIZBCLKZMKZAUHMKZUBUEDEU
    CUDUFUINAOUEBCPQUBUCUIUJNUDUBUGAUHMARSTUA $.
    $( [13-Feb-2006] $)

  ${
    his35.1 $e |- A e. CC $.
    his35.2 $e |- B e. CC $.
    his35.3 $e |- C e. ~H $.
    his35.4 $e |- D e. ~H $.
    $( Move scalar multiplication to outside of inner product. $)
    his35i $p |- ( ( A .h C ) .ih ( B .h D ) ) =
              ( ( A x. ( * ` B ) ) x. ( C .ih D ) ) $=
      ( csm co csp cmul ccj cfv cc wcel chil wceq his5 mp3an opreq2i hvmulcli
      ax-his3 cjcli hicli mulassi 3eqtr4i ) ACBDIJZKJZLJZABMNZCDKJZLJZLJACIJUHK
      JZAUKLJULLJUIUMALBOPCQPZDQPUIUMRFGHBCDSTUAAOPUOUHQPUNUJREGBDFHUBACUHUCTAU
      KULEBFUDCDGHUEUFUG $.
      $( [1-Jul-2005] $)
  $}

  $( Distributive law for inner product.  Lemma 3.1(S7) of [Beran] p. 95. $)
  his7 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                ( A .ih ( B +h C ) ) = ( ( A .ih B ) + ( A .ih C ) ) ) $=
    ( chil wcel w3a cva co csp ccj cfv caddc wceq ax-his2 fveq2d wa cc hicl
    cjadd syl2an 3impdir eqtrd 3comr hvaddcl ax-his1 sylan2 3impb 3adant3
    3adant2 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZBCGHZAIHZJKZBAIHZJKZCAIHZJKZLHZAUP
    IHZABIHZACIHZLHUMUNULURVCMUMUNULFZURUSVALHZJKZVCVGUQVHJBCANOUMULUNVIVCMZUMU
    LPUSQEVAQEVJUNULPBARCARUSVASTUAUBUCULUMUNVDURMZUMUNPULUPDEVKBCUDAUPUEUFUGUO
    VEUTVFVBLULUMVEUTMUNABUEUHULUNVFVBMUMACUEUIUJUK $.
    $( [31-Jul-1999] $)

  $( Distributive/associative law for inner product, useful for linearity
     proofs. $)
  hiassdi $p |- ( ( ( A e. CC /\ B e. ~H ) /\ ( C e. ~H /\ D e. ~H ) ) ->
    ( ( ( A .h B ) +h C ) .ih D ) = ( ( A x. ( B .ih D ) ) + ( C .ih D ) ) ) $=
    ( cc wcel chil wa csm co cva csp caddc cmul wceq hvmulcl ax-his2 3expb
    sylan ax-his3 3expa adantrl opreq1d eqtrd ) AEFZBGFZHZCGFZDGFZHZHZABIJZCKJD
    LJZULDLJZCDLJZMJZABDLJNJZUOMJUGULGFZUJUMUPOZABPURUHUIUSULCDQRSUKUNUQUOMUGUI
    UNUQOZUHUEUFUIUTABDTUAUBUCUD $.
    $( [10-May-2005] $)

  $( Distributive law for inner product of vector subtraction. $)
  his2sub $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                ( ( A -h B ) .ih C ) = ( ( A .ih C ) - ( B .ih C ) ) ) $=
    ( chil wcel w3a cmv co csp c1 cneg csm cva caddc cmin wceq wa hvsubval
    opreq1d 3adant3 cc ax1cn negcli hvmulcl mpan ax-his2 syl3an2 cmul ax-his3
    mp3an1 hicl mulm1 syl eqtrd opreq2d 3adant1 3adant2 negsub syl11anc 3eqtrd
    ) ADEZBDEZCDEZFZABGHZCIHZAJKZBLHZMHZCIHZACIHZBCIHZKZNHZVKVLOHZVAVBVFVJPVCVA
    VBQVEVICIABRSTVDVJVKVHCIHZNHZVNVBVAVHDEZVCVJVQPVGUAEZVBVRJUBUCZVGBUDUEAVHCU
    FUGVBVCVQVNPVAVBVCQZVPVMVKNWAVPVGVLUHHZVMVSVBVCVPWBPVTVGBCUIUJWAVLUAEZWBVMP
    BCUKZVLULUMUNUOUPUNVDVKUAEZWCVNVOPVAVCWEVBACUKUQVBVCWCVAWDUPVKVLURUSUT $.
    $( [1-Aug-2006] $) $( [16-Nov-1999] $)

  $( Distributive law for inner product of vector subtraction. $)
  his2sub2 $p |- ( ( A e. ~H /\ B e. ~H /\ C e. ~H ) ->
                ( A .ih ( B -h C ) ) = ( ( A .ih B ) - ( A .ih C ) ) ) $=
    ( chil wcel w3a cmv co csp ccj cfv cmin wceq his2sub fveq2d wa cc hicl
    cjsub syl2an 3impdir eqtrd 3comr hvsubcl ax-his1 sylan2 3impb 3adant3
    3adant2 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZBCGHZAIHZJKZBAIHZJKZCAIHZJKZLHZAUP
    IHZABIHZACIHZLHUMUNULURVCMUMUNULFZURUSVALHZJKZVCVGUQVHJBCANOUMULUNVIVCMZUMU
    LPUSQEVAQEVJUNULPBARCARUSVASTUAUBUCULUMUNVDURMZUMUNPULUPDEVKBCUDAUPUEUFUGUO
    VEUTVFVBLULUMVEUTMUNABUEUHULUNVFVBMUMACUEUIUJUK $.
    $( [13-Feb-2006] $)

  $( A necessary and sufficient condition for an inner product to be real. $)
  hire $p |- ( ( A e. ~H /\ B e. ~H ) ->
              ( ( A .ih B ) e. RR <-> ( A .ih B ) = ( B .ih A ) ) ) $=
    ( chil wcel wa csp co cr ccj cfv wceq cc wb hicl cjreb syl eqcom syl6bb
    ax-his1 ancoms eqeq2d bitr4d ) ACDZBCDZEZABFGZHDZUFUFIJZKZUFBAFGZKUEUGUHUFK
    ZUIUEUFLDUGUKMABNUFOPUHUFQRUEUJUHUFUDUCUJUHKBASTUAUB $.
    $( [2-Jul-2005] $)

  $( Real closure of inner product with self. $)
  hiidrcl $p |- ( A e. ~H -> ( A .ih A ) e. RR ) $=
    ( chil wcel csp co cr wa wceq eqid hire mpbiri anidms ) ABCZAADEZFCZMMGONNH
    NIAAJKL $.
    $( [29-May-1999] $)

  $( Inner product with the 0 vector. $)
  hi01 $p |- ( A e. ~H -> ( 0h .ih A ) = 0 ) $=
    ( chil wcel c0v csp co cc0 cmul csm wceq ax-hv0cl ax-hvmul0 ax-mp opreq1i
    cc 0cn ax-his3 mp3an12 syl5eqr hicl mpan mul02 syl eqtrd ) ABCZDAEFZGUFHFZG
    UEUFGDIFZAEFZUGUHDAEDBCZUHDJKDLMNGOCUJUEUIUGJPKGDAQRSUEUFOCZUGGJUJUEUKKDATU
    AUFUBUCUD $.
    $( [29-May-1999] $)

  $( Inner product with the 0 vector. $)
  hi02 $p |- ( A e. ~H -> ( A .ih 0h ) = 0 ) $=
    ( chil wcel c0v csp co ccj cfv cc0 wceq ax-hv0cl ax-his1 mpan2 hi01 fveq2d
    cj0 syl6eq eqtrd ) ABCZADEFZDAEFZGHZISDBCTUBJKADLMSUBIGHISUAIGANOPQR $.
    $( [13-Oct-1999] $)

  $( Inner product with self is not negative. $)
  hiidge0 $p |- ( A e. ~H -> 0 <_ ( A .ih A ) ) $=
    ( chil wcel cc0 csp co cle wbr clt wceq wo c0v wn pm2.1 wne df-ne ax-his4
    sylan2br ex wa opreq1 hi01 sylan9eqr eqcomd orim12d mpi cr wb 0re hiidrcl
    leloe sylancr mpbird ) ABCZDAAEFZGHZDUOIHZDUOJZKZUNALJZMZUTKUSUTNUNVAUQUTUR
    UNVAUQVAUNALOUQALPAQRSUNUTURUNUTTUODUTUNUOLAEFDALAEUAAUBUCUDSUEUFUNDUGCUOUG
    CUPUSUHUIAUJDUOUKULUM $.
    $( [29-May-1999] $)

  $( Zero inner product with self means vector is zero.  Lemma 3.1(S6) of
     [Beran] p. 95. $)
  his6 $p |- ( A e. ~H -> ( ( A .ih A ) = 0 <-> A = 0h ) ) $=
    ( chil wcel csp co cc0 wceq c0v wne wa clt wbr ax-his4 wb cr cle hiidrcl
    hiidge0 0re leltne mp3an1 syl11anc adantr mpbid ex necon4d hi01 opreq1
    eqeq1d syl5ibrcom impbid ) ABCZAADEZFGZAHGZULAHUMFULAHIZUMFIZULUPJFUMKLZUQA
    MULURUQNZUPULUMOCZFUMPLZUSAQARFOCUTVAUSSFUMTUAUBUCUDUEUFULUNUOHADEZFGAUGUOU
    MVBFAHADUHUIUJUK $.
    $( [27-Jul-1999] $)

  ${
    his1.1 $e |- A e. ~H $.
    his1.2 $e |- B e. ~H $.
    $( Conjugate law for inner product.  Postulate (S1) of [Beran] p. 95. $)
    his1i $p |- ( A .ih B ) = ( * ` ( B .ih A ) ) $=
      ( chil wcel csp co ccj cfv wceq ax-his1 mp2an ) AEFBEFABGHBAGHIJKCDABLM
      $.
      $( [15-May-2005] $)
  $}

  $( Commuted inner products have the same absolute values. $)
  abshicom $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( abs ` ( A .ih B ) ) = ( abs ` ( B .ih A ) ) ) $=
    ( chil wcel wa csp co cabs cfv ccj ax-his1 fveq2d cc wceq hicl ancoms
    abscj syl eqtrd ) ACDZBCDZEZABFGZHIBAFGZJIZHIZUDHIZUBUCUEHABKLUBUDMDZUFUGNU
    ATUHBAOPUDQRS $.
    $( [26-May-2006] $)

  ${
    $d x A $.
    $( A vector whose inner product is always zero is zero. $)
    hial0 $p |- ( A e. ~H -> ( A. x e. ~H ( A .ih x ) = 0 <-> A = 0h ) ) $=
      ( chil wcel cv csp co cc0 wceq wral c0v opreq2 eqeq1d rcla4v his6 sylibd
      wi opreq1 hi01 sylan9eq ex a1i r19.21adv impbid ) BCDZBAEZFGZHIZACJZBKIZU
      EUIBBFGZHIZUJUHULABCUFBIUGUKHUFBBFLMNBOPUEUJUHACUJUFCDZUHQQUEUJUMUHUJUMUG
      KUFFGHBKUFFRUFSTUAUBUCUD $.
      $( [24-Oct-1999] $)

    $( A vector whose inner product is always zero is zero. $)
    hial02 $p |- ( A e. ~H -> ( A. x e. ~H ( x .ih A ) = 0 <-> A = 0h ) ) $=
      ( chil wcel cv csp co cc0 wceq wral c0v opreq1 eqeq1d rcla4v his6 sylibd
      wi opreq2 hi02 sylan9eq ex a1i r19.21adv impbid ) BCDZAEZBFGZHIZACJZBKIZU
      EUIBBFGZHIZUJUHULABCUFBIUGUKHUFBBFLMNBOPUEUJUHACUJUFCDZUHQQUEUJUMUHUJUMUG
      UFKFGHBKUFFRUFSTUAUBUCUD $.
      $( [28-Jan-2006] $)
  $}

  ${
    hisubcom.1 $e |- A e. ~H $.
    hisubcom.2 $e |- B e. ~H $.
    hisubcom.3 $e |- C e. ~H $.
    hisubcom.4 $e |- D e. ~H $.
    $( Two vector subtractions simultaneously commute in an inner product. $)
    hisubcomi $p |- ( ( A -h B ) .ih ( C -h D ) ) =
                   ( ( B -h A ) .ih ( D -h C ) ) $=
      ( c1 cneg cmv co csm csp hvnegdii opreq12i ccj cfv cmul ax1cn negcli
      hvsubcli his35i cr wcel wceq 1re renegcli cjre ax-mp opreq2i mul2negi
      mulid1i 3eqtri opreq1i hicli mulid2i eqtr3i ) IJZBAKLZMLZUSDCKLZMLZNLZABK
      LZCDKLZNLUTVBNLZVAVEVCVFNBAFEODCHGOPVDUSUSQRZSLZVGSLIVGSLVGUSUSUTVBITUAZV
      JBAFEUBZDCHGUBZUCVIIVGSVIUSUSSLIISLIVHUSUSSUSUDUEVHUSUFIUGUHUSUIUJUKIITTU
      LITUMUNUOVGUTVBVKVLUPUQUNUR $.
      $( [1-Jul-2005] $)
  $}

  $( Lemma used to prove equality of vectors. $)
  hi2eq $p |- ( ( A e. ~H /\ B e. ~H ) ->
               ( ( A .ih ( A -h B ) ) = ( B .ih ( A -h B ) ) <-> A = B ) ) $=
    ( chil wcel wa cmv co csp cmin cc0 wceq c0v hvsubcl his2sub mpd3an3 eqeq1d
    wb his6 syl bitr3d cc hicl syldan simpr syl11anc subeq0 hvsubeq0 3bitr3d )
    ACDZBCDZEZAABFGZHGZBULHGZIGZJKZULLKZUMUNKZABKUKULULHGZJKZUPUQUKUSUOJUIUJULC
    DZUSUOKABMZABULNOPUKVAUTUQQVBULRSTUKUMUADZUNUADZUPURQUIUJVAVCVBAULUBUCUKUJV
    AVDUIUJUDVBBULUBUEUMUNUFUEABUGUH $.
    $( [16-Nov-1999] $)

  ${
    $d x A $.  $d x B $.
    $( Two vectors whose inner product is always equal are equal. $)
    hial2eq $p |- ( ( A e. ~H /\ B e. ~H ) ->
                   ( A. x e. ~H ( A .ih x ) = ( B .ih x ) <-> A = B ) ) $=
      ( chil wcel wa cv csp co wceq wral cmv wi hvsubcl opreq2 eqeq12d rcla4v
      syl hi2eq sylibd opreq1 a1d r19.21aiv impbid1 ) BDECDEFZBAGZHIZCUFHIZJZAD
      KZBCJZUEUJBBCLIZHIZCULHIZJZUKUEULDEUJUOMBCNUIUOAULDUFULJUGUMUHUNUFULBHOUF
      ULCHOPQRBCSTUKUIADUKUIUFDEBCUFHUAUBUCUD $.
      $( [16-Nov-1999] $)

    $( Two vectors whose inner product is always equal are equal. $)
    hial2eq2 $p |- ( ( A e. ~H /\ B e. ~H ) ->
                   ( A. x e. ~H ( x .ih A ) = ( x .ih B ) <-> A = B ) ) $=
      ( chil wcel wa cv csp co wceq wral wb ccj cfv ax-his1 eqeqan12d cc hicl
      ancoms cj11 syl2an bitr2d anandirs ralbidva hial2eq bitrd ) BDEZCDEZFZAGZ
      BHIZUJCHIZJZADKBUJHIZCUJHIZJZADKBCJUIUMUPADUGUHUJDEZUMUPLUGUQFZUHUQFZFUPU
      KMNZULMNZJZUMURUSUNUTUOVABUJOCUJOPURUKQEZULQEZVBUMLUSUQUGVCUJBRSUQUHVDUJC
      RSUKULTUAUBUCUDABCUEUF $.
      $( [28-Jan-2006] $)
  $}

  $( Orthogonality commutes. $)
  orthcom $p |- ( ( A e. ~H /\ B e. ~H ) ->
                ( ( A .ih B ) = 0 <-> ( B .ih A ) = 0 ) ) $=
    ( chil wcel wa csp co cc0 wceq ccj cfv fveq2 cj0 syl6eq ax-his1 ancoms
    eqeq1d syl5ibr impbid ) ACDZBCDZEZABFGZHIZBAFGZHIZUDUFUBUCJKZHIUDUGHJKZHUCH
    JLMNUBUEUGHUATUEUGIBAOPQRUFUDUBUEJKZHIUFUIUHHUEHJLMNUBUCUIHABOQRS $.
    $( [10-Oct-1999] $)

  ${
    normlem1.1 $e |- S e. CC $.
    normlem1.2 $e |- F e. ~H $.
    normlem1.3 $e |- G e. ~H $.
    $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
       [Beran] p. 97. $)
    normlem0 $p |- ( ( F -h ( S .h G ) ) .ih
                         ( F -h ( S .h G ) ) ) =
                ( ( ( F .ih F ) + ( -u ( * ` S ) x. ( F .ih G ) ) ) +
  ( ( -u S x. ( G .ih F ) ) + ( ( S x. ( * ` S ) ) x. ( G .ih G ) ) ) ) $=
      ( csm co cmv csp cneg cva caddc ccj cfv cmul c1 hvmulcli hvsubvali
      mulm1i opreq1i ax1cn negcli hvmulassi eqtr3i opreq2i eqtr4i opreq12i
      chil wcel wceq hvaddcli ax-his2 mp3an his7 cc his5 cjnegi eqtri ax-his3
      hicli cjcli mulcli adddii mulassi mul2negi 3eqtri ) BACGHZIHZVIJHBAKZCGHZ
      LHZVLJHZBVLJHZVKVLJHZMHZBBJHZANOZKZBCJHZPHZMHZVJCBJHZPHZAVRPHZCCJHZPHZMHZ
      MHVIVLVIVLJVIBQKZVHGHZLHVLBVHEACDFRSVKWJBLWIAPHZCGHVKWJWKVJCGADTUAWIACQUB
      UCDFUDUEUFUGZWLUHBUIUJZVKUIUJZVLUIUJZVMVPUKEVJCADUCZFRZBVKEWQULZBVKVLUMUN
      VNWBVOWHMVNVQBVKJHZMHZWBWMWMWNVNWTUKEEWQBBVKUOUNWSWAVQMWSVJNOZVTPHZWAVJUP
      UJZWMCUIUJZWSXBUKWPEFVJBCUQUNXAVSVTPADURZUAUSUFUSVOVJCVLJHZPHZVJWCXAWFPHZ
      MHZPHZWHXCXDWOVOXGUKWPFWRVJCVLUTUNXFXIVJPXFWCCVKJHZMHZXIXDWMWNXFXLUKFEWQC
      BVKUOUNXKXHWCMXCXDXDXKXHUKWPFFVJCCUQUNUFUSUFXJWDVJXHPHZMHWHVJWCXHWPCBFEVA
      XAWFVJWPVBZCCFFVAZVCVDXMWGWDMVJXAPHZWFPHXMWGVJXAWFWPXNXOVEXPWEWFPXPVJVSPH
      WEXAVSVJPXEUFAVRDADVBVFUSUAUEUFUSVGUHVG $.
      $( [7-Oct-1999] $)

    ${
      normlem1.4 $e |- R e. RR $.
      normlem1.5 $e |- ( abs ` S ) = 1 $.
      $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
         [Beran] p. 97. $)
      normlem1 $p |- ( ( F -h ( ( S x. R ) .h G ) ) .ih
                         ( F -h ( ( S x. R ) .h G ) ) ) =
                ( ( ( F .ih F ) + ( ( ( * ` S ) x. -u R ) x. ( F .ih G ) ) ) +
  ( ( ( S x. -u R ) x. ( G .ih F ) ) + ( ( R ^ 2 ) x. ( G .ih G ) ) ) ) $=
        ( cmul co csm cmv csp ccj cfv cneg caddc c2 cexp recni mulcli normlem0
        cjmuli cr wcel wceq cjrebi mpbi opreq2i eqtri negeqi cjcli mulneg2i
        eqtr4i opreq1i eqcomi mul4i c1 cabs absvalsqi sq1 3eqtr3i opreq12i
        mulid2i 3eqtri sqvali ) CBAJKZDLKMKZVINKCCNKZVHOPZQZCDNKZJKZRKZVHQZDCNK
        ZJKZVHVKJKZDDNKZJKZRKZRKVJBOPZAQZJKZVMJKZRKZBWDJKZVQJKZASTKZVTJKZRKZRKV
        HCDBAEAHUAZUBFGUCVOWGWBWLRVNWFVJRVLWEVMJVLWCAJKZQWEVKWNVKWCAOPZJKZWNBAE
        WMUDZWOAWCJAUEUFWOAUGHAWMUHUIZUJUKULWCABEUMZWMUNUOUPUJVRWIWAWKRVPWHVQJW
        HVPBAEWMUNUQUPVSWJVTJVSAAJKZWJVSVHWPJKBWCJKZAWOJKZJKZWTVKWPVHJWQUJBAWCW
        OEWMWSAWMUMURXCUSWTJKWTXAUSXBWTJBUTPZSTKUSSTKXAUSXDUSSTIUPBEVAVBVCWOAAJ
        WRUJVDWTAAWMWMUBVEUKVFAWMVGUOUPVDVDUK $.
        $( [22-Aug-1999] $)
    $}

    ${
      normlem2.4 $e |- B = -u ( ( ( * ` S ) x. ( F .ih G ) ) +
                    ( S x. ( G .ih F ) ) ) $.
      $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
         [Beran] p. 97. $)
      normlem2 $p |- B e. RR $=
        ( ccj cfv csp co cmul caddc cneg cr wcel wceq cjcji eqcomi his1i
        opreq12i cjcli hicli cjmuli eqtr4i opreq2i mulcli addcomi cjaddi
        3eqtr4ri addcli cjrebi mpbir renegcli eqeltri ) ABIJZCDKLZMLZBDCKLZMLZN
        LZOPHVBVBPQVBIJZVBRVAUSNLUSIJZVAIJZNLVBVCVAVDUSVENVAUQIJZURIJZMLVDBVFUT
        VGMVFBBESTDCGFUAUBUQURBEUCZCDFGUDZUEUFUSUQUTIJZMLVEURVJUQMCDFGUAUGBUTED
        CGFUDZUEUFUBUSVAUQURVHVIUHZBUTEVKUHZUIUSVAVLVMUJUKVBUSVAVLVMULUMUNUOUP
        $.
        $( [27-Jul-1999] $)

      ${
        normlem3.5 $e |- A = ( G .ih G ) $.
        normlem3.6 $e |- C = ( F .ih F ) $.
        ${
          normlem3.7 $e |- R e. RR $.
          $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii)
             of [Beran] p. 97. $)
          normlem3 $p |- ( ( ( A x. ( R ^ 2 ) ) + ( B x. R ) ) + C ) =
                ( ( ( F .ih F ) + ( ( ( * ` S ) x. -u R ) x. ( F .ih G ) ) ) +
  ( ( ( S x. -u R ) x. ( G .ih F ) ) + ( ( R ^ 2 ) x. ( G .ih G ) ) ) ) $=
            ( c2 cexp co cmul caddc csp ccj cfv cneg cc hicli eqeltri recni
            sqcli mulcli normlem2 addcomi cjcli addcli mulneg1i opreq1i
            mulneg2i 3eqtr4i negcli adddiri mul32i opreq12i 3eqtri mulcomi
            opreq2i eqtr3i addassi ) CADOPQZRQZBDRQZSQZSQFFTQZEUAUBZDUCZRQZFGTQ
            ZRQZEVMRQZGFTQZRQZVGGGTQZRQZSQZSQZSQVJCSQVKVPSQWBSQCVKVJWCSMVJVIVHS
            QVPVSSQZWASQWCVHVIAVGAVTUDLGGJJUEZUFZDDNUGZUHZUIZBDBBEFGHIJKUJUGWGU
            IZUKVIWDVHWASVIVLVORQZEVRRQZSQZVMRQZWKVMRQZWLVMRQZSQWDWMUCZDRQWMDRQ
            UCVIWNWMDWKWLVLVOEHULZFGIJUEZUIZEVRHGFJIUEZUIZUMZWGUNBWQDRKUOWMDXCW
            GUPUQWKWLVMWTXBDWGURZUSWOVPWPVSSVLVOVMWRWSXDUTEVRVMHXAXDUTVAVBVGARQ
            VHWAVGAWHWFVCAVTVGRLVDVEVAVPVSWAVNVOVLVMWRXDUIWSUIZVQVREVMHXDUIXAUI
            ZVGVTWHWEUIZVFVBVAVJCVHVIWIWJUMCVKUDMFFIIUEZUFUKVKVPWBXHXEVSWAXFXGU
            MVFUQ $.
            $( [21-Aug-1999] $)
        $}


        ${
          normlem4.7 $e |- R e. RR $.
          normlem4.8 $e |- ( abs ` S ) = 1 $.
          $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii)
             of [Beran] p. 97. $)
          normlem4 $p |- ( ( F -h ( ( S x. R ) .h G ) ) .ih
                       ( F -h ( ( S x. R ) .h G ) ) ) =
                       ( ( ( A x. ( R ^ 2 ) ) + ( B x. R ) ) + C ) $=
            ( cmul co csm cmv csp ccj cfv cneg caddc c2 cexp normlem1 normlem3
            eqtr4i ) FEDPQGRQSQZUJTQFFTQEUAUBDUCZPQFGTQPQUDQEUKPQGFTQPQDUEUFQZG
            GTQPQUDQUDQAULPQBDPQUDQCUDQDEFGHIJNOUGABCDEFGHIJKLMNUHUI $.
            $( [29-Jul-1999] $)

          $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii)
             of [Beran] p. 97. $)
          normlem5 $p |- 0 <_
                       ( ( ( A x. ( R ^ 2 ) ) + ( B x. R ) ) + C ) $=
            ( cc0 cmul co csm cmv csp c2 cexp caddc cle chil wcel wbr recni
            mulcli hvmulcli hvsubcli hiidge0 ax-mp normlem4 breqtri ) PFEDQRZGS
            RZTRZUSUARZADUBUCRQRBDQRUDRCUDRUEUSUFUGPUTUEUHFURIUQGEDHDNUIUJJUKUL
            USUMUNABCDEFGHIJKLMNOUOUP $.
            $( [10-Aug-1999] $)
        $}

        ${
          $d x A $.  $d x B $.  $d x C $.
          normlem6.7 $e |- ( abs ` S ) = 1 $.
          $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii)
             of [Beran] p. 97. $)
          normlem6 $p |- ( abs ` B ) <_
                       ( 2 x. ( ( sqr ` A ) x. ( sqr ` C ) ) ) $=
            ( vx c2 cexp co csqr cfv c4 cmul cabs cle wbr cc0 caddc cmin csp
            chil wcel hiidge0 ax-mp breqtrri cr hiidrcl eqeltri normlem2 cv
            cif wceq opreq1 opreq2d opreq2 opreq12d opreq1d breq2d 0re elimel
            normlem5 dedth rgen discrlem resqcli 4re remulcli lesubadd2i mpbi
            recni addid1i breqtri wb sqge0i 4pos ltleii mulge0i mp2an sqrlei
            absrei sqrmulii sqr4 opreq12i eqtr2i 3brtr4i ) BOPQZRSZTACUAQZUAQZR
            SZBUBSOARSCRSUAQZUAQZUCWNWQUCUDZWOWRUCUDZWNWQUEUFQZWQUCWNWQUGQUEUCU
            DZWNXCUCUDUEAUCUDZXDUEFFUHQZAUCFUIUJZUEXFUCUDIFUKULKUMZNABCAXFUNKXG
            XFUNUJIFUOULUPZBDEFGHIJUQZCEEUHQZUNLEUIUJZXKUNUJHEUOULUPZUEANURZOPQ
            ZUAQZBXNUAQZUFQZCUFQZUCUDZNUNXNUNUJZXTUEAYAXNUEUSZOPQZUAQZBYBUAQZUF
            QZCUFQZUCUDXNUEXNYBUTZXSYGUEUCYHXRYFCUFYHXPYDXQYEUFYHXOYCAUAXNYBOPV
            AVBXNYBBUAVCVDVEVFABCYBDEFGHIJKLXNUEUNVGVHMVIVJVKVLULWNWQUEBXJVMZTW
            PVNACXIXMVOZVOZVGVPVQWQWQYKVRVSVTUEWNUCUDUEWQUCUDZXAXBWABXJWBUETUCU
            DUEWPUCUDZYLUETVGVNWCWDZXEUECUCUDYMXHUEXKCUCXLUEXKUCUDHEUKULLUMZACX
            IXMWEWFZTWPVNYJWEWFWNWQYIYKWGWFVQBXJWHWRTRSZWPRSZUAQWTTWPVNYJYNYPWI
            YQOYRWSUAWJACXIXMXHYOWIWKWLWM $.
            $( [2-Aug-1999] $)

        $}
      $}
    $}

    ${
      normlem7.4 $e |- ( abs ` S ) = 1 $.
      $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
         [Beran] p. 97. $)
      normlem7 $p |- ( ( ( * ` S ) x. ( F .ih G ) ) + ( S x. ( G .ih F ) ) ) <_
                ( 2 x. ( ( sqr ` ( G .ih G ) ) x. ( sqr ` ( F .ih F ) ) ) ) $=
        ( ccj cfv csp co cmul caddc cneg cabs cle wbr c2 csqr cr wcel eqid
        normlem2 cjcli hicli mulcli addcli negrebi mpbi leabsi absnegi
        breqtrri normlem6 negcli abscli 2re cc0 chil hiidge0 ax-mp hiidrcl
        sqrcli remulcli letri mp2an ) AHIZBCJKZLKZACBJKZLKZMKZVKNZOIZPQVMRCCJKZ
        SIZBBJKZSIZLKZLKZPQVKVSPQVKVKOIVMPVKVLTUAVKTUAVLABCDEFVLUBZUCVKVHVJVFVG
        ADUDBCEFUEUFAVIDCBFEUEUFUGZUHUIZUJVKWAUKULVNVLVPABCDEFVTVNUBVPUBGUMVKVM
        VSWBVLVKWAUNUORVRUPVOVQUQVNPQZVOTUACURUAZWCFCUSUTVNWDVNTUAFCVAUTVBUTUQV
        PPQZVQTUABURUAZWEEBUSUTVPWFVPTUAEBVAUTVBUTVCVCVDVE $.
        $( [11-Aug-1999] $)
    $}
  $}

  ${
    normlem8.1 $e |- A e. ~H $.
    normlem8.2 $e |- B e. ~H $.
    normlem8.3 $e |- C e. ~H $.
    normlem8.4 $e |- D e. ~H $.
    $( Lemma used to derive properties of norm. $)
    normlem8 $p |- ( ( A +h B ) .ih ( C +h D ) ) =
        ( ( ( A .ih C ) + ( B .ih D ) ) + ( ( A .ih D ) + ( B .ih C ) ) ) $=
      ( cva co csp caddc chil wcel wceq his7 mp3an opreq12i hvaddcli ax-his2
      hicli add42i 3eqtr4i ) ACDIJZKJZBUDKJZLJZACKJZADKJZLJZBCKJZBDKJZLJZLJABIJ
      UDKJZUHULLJUIUKLJLJUEUJUFUMLAMNZCMNZDMNZUEUJOEGHACDPQBMNZUPUQUFUMOFGHBCDP
      QRUOURUDMNUNUGOEFCDGHSABUDTQUHULUIUKACEGUABDFHUAADEHUABCFGUAUBUC $.
      $( [30-Jun-2005] $)

    $( Lemma used to derive properties of norm. $)
    normlem9 $p |- ( ( A -h B ) .ih ( C -h D ) ) =
        ( ( ( A .ih C ) + ( B .ih D ) ) - ( ( A .ih D ) + ( B .ih C ) ) ) $=
      ( cmv co csp c1 cneg csm cva caddc cmin hvsubvali opreq12i ax1cn negcli
      hvmulcli normlem8 cmul ccj cfv cc wcel chil wceq ax-his3 mp3an his5
      opreq2i cr 1re renegcli cjre ax-mp mul2negi mulid2i 3eqtri opreq1i cjcli
      hicli mulassi 3eqtr3i mulm1i eqtri negdii eqtr4i addcli negsubi ) ABIJZCD
      IJZKJALMZBNJZOJZCVPDNJZOJZKJACKJZVQVSKJZPJZAVSKJZVQCKJZPJZPJZWABDKJZPJZAD
      KJZBCKJZPJZQJZVNVRVOVTKABEFRCDGHRSAVQCVSEVPBLTUAZFUBGVPDWNHUBZUCWGWIWLMZP
      JWMWCWIWFWPPWBWHWAPWBVPBVSKJZUDJZVPVPUEUFZWHUDJZUDJZWHVPUGUHZBUIUHZVSUIUH
      WBWRUJWNFWOVPBVSUKULWQWTVPUDXBXCDUIUHZWQWTUJWNFHVPBDUMULUNVPWSUDJZWHUDJLW
      HUDJXAWHXELWHUDXEVPVPUDJLLUDJLWSVPVPUDVPUOUHWSVPUJLUPUQVPURUSZUNLLTTUTLTV
      AVBVCVPWSWHWNVPWNVDBDFHVEZVFWHXGVAVGVBUNWFWJMZWKMZPJWPWDXHWEXIPWDWSWJUDJZ
      VPWJUDJXHXBAUIUHXDWDXJUJWNEHVPADUMULWSVPWJUDXFVCWJADEHVEZVHVBWEVPWKUDJZXI
      XBXCCUIUHWEXLUJWNFGVPBCUKULWKBCFGVEZVHVISWJWKXKXMVJVKSWIWLWAWHACEGVEXGVLW
      JWKXKXMVLVMVIVB $.
      $( [30-Jun-2005] $)
  $}

  ${
    normlem7t.1 $e |- A e. ~H $.
    normlem7t.2 $e |- B e. ~H $.
    $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
       [Beran] p. 97. $)
    normlem7tALT $p |- ( ( S e. CC /\ ( abs ` S ) = 1 ) ->
              ( ( ( * ` S ) x. ( A .ih B ) ) + ( S x. ( B .ih A ) ) ) <_
              ( 2 x. ( ( sqr ` ( B .ih B ) ) x. ( sqr ` ( A .ih A ) ) ) ) ) $=
      ( cc wcel cabs cfv c1 wceq wa ccj csp co cmul caddc c2 csqr cle wbr cif
      fveq2 opreq1d opreq1 opreq12d breq1d eleq1 eqeq1d anbi12d ax1cn cc0 0re
      1re lt01 ltleii absidi ax-mp pm3.2i elimhyp simpli simpri normlem7 dedth
      ) CFGZCHIZJKZLZCMIZABNOZPOZCBANOZPOZQOZRBBNOSIAANOSIPOPOZTUAVHCJUBZMIZVJP
      OZVPVLPOZQOZVOTUACJCVPKZVNVTVOTWAVKVRVMVSQWAVIVQVJPCVPMUCUDCVPVLPUEUFUGVP
      ABVPFGZVPHIZJKZVHWBWDLJFGZJHIZJKZLCJWAVEWBVGWDCVPFUHWAVFWCJCVPHUCUIUJJVPK
      ZWEWBWGWDJVPFUHWHWFWCJJVPHUCUIUJWEWGUKULJTUAWGULJUMUNUOUPJUNUQURUSUTZVADE
      WBWDWIVBVCVD $.
      $( [11-Oct-1999] $)

    $( Equality case of Bunjakovaskij-Cauchy-Schwarz inequality.  Specifically,
       in the equality case the two vectors are collinear.  Compare
       ~ bcsiHIL . $)
    bcseqi $p |- ( ( ( A .ih B ) x. ( B .ih A ) ) = ( ( A .ih A ) x.
             ( B .ih B ) ) <-> ( ( B .ih B ) .h A ) = ( ( A .ih B ) .h B ) ) $=
      ( csp co cmul wceq csm cmv c0v cc0 caddc cmin hicli hvmulcli normlem9
      opreq1 eqcomd ccj cfv cc wcel chil his5 mp3an cr hiidrcl ax-mp cjre
      ax-his3 opreq12i mulcli mulcomi opreq1i eqtr4i 3eqtri his1i eqcomi
      mulassi 3eqtr4g mul32i 3eqtr4ri a1i opreq12d opreq1d addcli subidi
      syl6eq syl5eq wb hvsubcli his6 sylib hvsubeq0i impbii ) ABEFZBAEFZGFZAAEF
      ZBBEFZGFZHZWAAIFZVQBIFZHZWCWDWEJFZKHZWFWCWGWGEFZLHZWHWCWIWDWDEFZWEWEEFZMF
      ZWDWEEFZWEWDEFZMFZNFZLWDWEWDWEWAABBDDOZCPZVQBABCDOZDPZWSXAQWCWQWPWPNFLWCW
      MWPWPNWCWKWNWLWOMWCWBWAGFZVSWAGFZWKWNWCXCXBVSWBWAGRSWKWATUAZWDAEFZGFZWAWA
      VTGFZGFZXBWAUBUCZWDUDUCZAUDUCZWKXFHWRWSCWAWDAUEUFXDWAXEXGGWAUGUCZXDWAHBUD
      UCZXLDBUHUIWAUJUIZXIXKXKXEXGHWRCCWAAAUKUFZULXHXGWAGFXBWAXGWRWAVTWRAACCOZU
      MUNWBXGWAGVTWAXPWRUNZUOUPUQWNVQTUAZWDBEFZGFZVRWAVQGFZGFZXCVQUBUCZXJXMWNXT
      HWTWSDVQWDBUEUFXRVRXSYAGVRXRBADCURUSZXIXKXMXSYAHWRCDWAABUKUFULYBYAVRGFWAV
      SGFZXCVRYABADCOZWAVQWRWTUMUNWAVQVRWRWTYFUTWAVSWRVQVRWTYFUMZUNUQUQVAWLWOHW
      CXDWEAEFZGFZYEWOWLXDWAYHVSGXNYCXMXKYHVSHWTDCVQBAUKUFZULXIWEUDUCZXKWOYIHWR
      XACWAWEAUEUFWLXRWEBEFZGFZVRVQWAGFZGFZYEYCYKXMWLYMHWTXADVQWEBUEUFXRVRYLYNG
      YDYCXMXMYLYNHWTDDVQBBUKUFULYOYNVRGFXCYEVRYNYFVQWAWTWRUMUNVQWAVRWTWRYFVBVS
      WAYGWRUNUQUQVCVDVEVFWPWNWOWDWEWSXAOWEWDXAWSOVGVHVIVJWGUDUCWJWHVKWDWEWSXAV
      LWGVMUIVNWDWEWSXAVOVNWFWBVSWFXEYHWBVSWDWEAERWBXGXEXQXOUPYHVSYJUSVASVP $.
      $( [16-Jul-2001] $)
  $}

  $( Lemma used to derive properties of norm.  Part of Remark 3.4(B) of [Beran]
     p. 98. $)
  normlem9at $p |- ( ( A e. ~H /\ B e. ~H ) -> ( ( A -h B ) .ih ( A -h B ) ) =
      ( ( ( A .ih A ) + ( B .ih B ) ) - ( ( A .ih B ) + ( B .ih A ) ) ) ) $=
    ( chil wcel cmv co csp caddc cmin wceq c0v cif opreq1 opreq12d id opreq1d
    opreq2 eqeq12d opreq2d ax-hv0cl elimel normlem9 dedth2h ) ACDZBCDZABEFZUFGF
    ZAAGFZBBGFZHFZABGFZBAGFZHFZIFZJUDAKLZBEFZUPGFZUOUOGFZUIHFZUOBGFZBUOGFZHFZIF
    ZJUOUEBKLZEFZVEGFZURVDVDGFZHFZUOVDGFZVDUOGFZHFZIFZJABKKAUOJZUGUQUNVCVMUFUPU
    FUPGAUOBEMZVNNVMUJUSUMVBIVMUHURUIHVMAUOAUOGVMOZVONPVMUKUTULVAHAUOBGMAUOBGQN
    NRBVDJZUQVFVCVLVPUPVEUPVEGBVDUOEQZVQNVPUSVHVBVKIVPUIVGURHVPBVDBVDGVPOZVRNSV
    PUTVIVAVJHBVDUOGQBVDUOGMNNRUOVDUOVDAKCTUAZBKCTUAZVSVTUBUC $.
    $( [10-May-2005] $)

