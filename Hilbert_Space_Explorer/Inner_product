$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Inner product
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Associative law for inner product.   Lemma 3.1(S5) of [Beran] p. 95. $)
  his5t $p |- ( ( A e. CC /\ B e. H~ /\ C e. H~ ) ->
                ( B .ih ( A .h C ) ) = ( ( * ` A ) x. ( B .ih C ) ) ) $=
    ( cc wcel chil w3a csm co csp ccj cfv cmul wceq wa ax-his1 hvmulclt sylan2
    3impb 3com12 ax-his3 3com23 fveq2d cjmult ax-hicl 3adant1 opreq2d eqtr4d
    3eqtrd ) ADEZBFEZCFEZGZBACHIZJIZUNBJIZKLZACBJIZMIZKLZAKLZBCJIZMIZUKUJULUOUQ
    NZUKUJULVDUKUNFEVDUJULOBUNPACQRSTUMUPUSKUJULUKUPUSNACBUAUBUCUMUTVAURKLZMIZV
    CUJULUKUTVFNZUJULUKVGUJURDEVGULUKOAURUDCBUERSUBUMVBVEVAMUKULVBVENUJBCPUFUGU
    HUI $.
    $( [29-Jul-1999] $)

  $( Associative law for inner product. $)
  his52t $p |- ( ( A e. CC /\ B e. H~ /\ C e. H~ ) ->
                ( B .ih ( ( * ` A ) .h C ) ) = ( A x. ( B .ih C ) ) ) $=
    ( cc wcel chil w3a ccj cfv csm co csp cmul wceq his5t cjclt syl3an1 cjcjt
    opreq1d 3ad2ant1 eqtrd ) ADEZBFEZCFEZGBAHIZCJKLKZUEHIZBCLKZMKZAUHMKZUEDEUCU
    DUFUINUBUEBCOAPQUBUCUIUJNUDUBUGAUHMARSTUA $.
    $( [19-Feb-2006] $) $( [13-Feb-2006] $)

  ${
    his35.1 $e |- A e. CC $.
    his35.2 $e |- B e. CC $.
    his35.3 $e |- C e. H~ $.
    his35.4 $e |- D e. H~ $.
    $( Move scalar multiplication to outside of inner product. $)
    his35 $p |- ( ( A .h C ) .ih ( B .h D ) ) =
              ( ( A x. ( * ` B ) ) x. ( C .ih D ) ) $=
      ( csm co csp cmul ccj cfv cc wcel chil wceq his5t mp3an opreq2i hvmulcl
      ax-his3 cjcl hicl mulass 3eqtr4 ) ACBDIJZKJZLJZABMNZCDKJZLJZLJACIJUHKJZAU
      KLJULLJUIUMALBOPCQPZDQPUIUMRFGHBCDSTUAAOPUOUHQPUNUJREGBDFHUBACUHUCTAUKULE
      BFUDCDGHUEUFUG $.
      $( [1-Jul-2005] $) $( [1-Jul-2005] $)
  $}

  $( Distributive law for inner product.  Lemma 3.1(S7) of [Beran] p. 95. $)
  his7t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                ( A .ih ( B +h C ) ) = ( ( A .ih B ) + ( A .ih C ) ) ) $=
    ( chil wcel w3a cva co csp ccj cfv caddc wceq ax-his2 fveq2d cc wa cjaddt
    ax-hicl syl2an 3impdir eqtrd 3comr ax-his1 hvaddclt sylan2 3impb 3adant3
    3adant2 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZBCGHZAIHZJKZBAIHZJKZCAIHZJKZLHZAUP
    IHZABIHZACIHZLHUMUNULURVCMUMUNULFZURUSVALHZJKZVCVGUQVHJBCANOUMULUNVIVCMZUSP
    EVAPEVJUMULQUNULQUSVARBASCASTUAUBUCULUMUNVDURMZULUPDEVKUMUNQAUPUDBCUEUFUGUO
    VEUTVFVBLULUMVEUTMUNABUDUHULUNVFVBMUMACUDUIUJUK $.
    $( [31-Jul-1999] $)

  $( Distributive/associative law for inner product, useful for linearity
     proofs. $)
  hiassdit $p |- ( ( ( A e. CC /\ B e. H~ ) /\ ( C e. H~ /\ D e. H~ ) ) ->
    ( ( ( A .h B ) +h C ) .ih D ) = ( ( A x. ( B .ih D ) ) + ( C .ih D ) ) ) $=
    ( cc wcel chil wa csm co cva csp caddc cmul wceq ax-his2 3expb hvmulclt
    sylan ax-his3 3expa adantrl opreq1d eqtrd ) AEFZBGFZHZCGFZDGFZHZHZABIJZCKJD
    LJZULDLJZCDLJZMJZABDLJNJZUOMJULGFZUJUMUPOZUGURUHUIUSULCDPQABRSUKUNUQUOMUGUI
    UNUQOZUHUEUFUIUTABDTUAUBUCUD $.
    $( [7-Jun-2005] $) $( [10-May-2005] $)

  $( Distributive law for inner product of vector subtraction. $)
  his2subt $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                ( ( A -h B ) .ih C ) = ( ( A .ih C ) - ( B .ih C ) ) ) $=
    ( chil wcel w3a cmv co csp c1 cneg csm cva caddc cmin wceq wa hvsubvalt
    opreq1d 3adant3 ax-his2 cc ax1cn negcl hvmulclt mpan syl3an2 cmul ax-his3
    mp3an1 ax-hicl mulm1t syl eqtrd opreq2d 3adant1 negsubt 3adant2 sylanc
    3eqtrd ) ADEZBDEZCDEZFZABGHZCIHZAJKZBLHZMHZCIHZACIHZBCIHZKZNHZVKVLOHZVAVBVF
    VJPVCVAVBQVEVICIABRSTVDVJVKVHCIHZNHZVNVAVHDEZVCVJVQPVBAVHCUAVGUBEZVBVRJUCUD
    ZVGBUEUFUGVBVCVQVNPVAVBVCQZVPVMVKNWAVPVGVLUHHZVMVSVBVCVPWBPVTVGBCUIUJWAVLUB
    EZWBVMPBCUKZVLULUMUNUOUPUNVKUBEZWCVNVOPVDVKVLUQVAVCWEVBACUKURVBVCWCVAWDUPUS
    UT $.
    $( [1-Aug-2006] $) $( [16-Nov-1999] $)

  $( Distributive law for inner product of vector subtraction. $)
  his2sub2t $p |- ( ( A e. H~ /\ B e. H~ /\ C e. H~ ) ->
                ( A .ih ( B -h C ) ) = ( ( A .ih B ) - ( A .ih C ) ) ) $=
    ( chil wcel w3a cmv co csp ccj cfv cmin wceq his2subt fveq2d cc wa cjsubt
    ax-hicl syl2an 3impdir eqtrd 3comr ax-his1 hvsubclt sylan2 3impb 3adant3
    3adant2 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZBCGHZAIHZJKZBAIHZJKZCAIHZJKZLHZAUP
    IHZABIHZACIHZLHUMUNULURVCMUMUNULFZURUSVALHZJKZVCVGUQVHJBCANOUMULUNVIVCMZUSP
    EVAPEVJUMULQUNULQUSVARBASCASTUAUBUCULUMUNVDURMZULUPDEVKUMUNQAUPUDBCUEUFUGUO
    VEUTVFVBLULUMVEUTMUNABUDUHULUNVFVBMUMACUDUIUJUK $.
    $( [19-Feb-2006] $) $( [13-Feb-2006] $)

  $( A necessary and sufficient condition for an inner product to be real. $)
  hiret $p |- ( ( A e. H~ /\ B e. H~ ) ->
              ( ( A .ih B ) e. RR <-> ( A .ih B ) = ( B .ih A ) ) ) $=
    ( chil wcel wa csp co cr ccj cfv wceq cc wb ax-hicl cjrebt syl eqcom
    syl6bb ax-his1 ancoms eqeq2d bitr4d ) ACDZBCDZEZABFGZHDZUFUFIJZKZUFBAFGZKUE
    UGUHUFKZUIUEUFLDUGUKMABNUFOPUHUFQRUEUJUHUFUDUCUJUHKBASTUAUB $.
    $( [3-Jul-2005] $) $( [2-Jul-2005] $)

  $( Real closure of inner product with self. $)
  hiidrclt $p |- ( A e. H~ -> ( A .ih A ) e. RR ) $=
    ( chil wcel csp co cr wa wceq eqid hiret mpbiri anidms ) ABCZAADEZFCZMMGONN
    HNIAAJKL $.
    $( [29-May-1999] $)

  $( Inner product with the 0 vector. $)
  hi01t $p |- ( A e. H~ -> ( 0h .ih A ) = 0 ) $=
    ( chil wcel c0v csp co cc0 cmul csm cc wceq 0cn ax-hv0cl ax-his3 mp3an12
    ax-hvmul0 ax-mp opreq1i syl5eqr ax-hicl mpan mul02t syl eqtrd ) ABCZDAEFZGU
    FHFZGUEGDIFZAEFZUGUFGJCDBCZUEUIUGKLMGDANOUHDAEUJUHDKMDPQRSUEUFJCZUGGKUJUEUK
    MDATUAUFUBUCUD $.
    $( [29-May-1999] $)

  $( Inner product with the 0 vector. $)
  hi02t $p |- ( A e. H~ -> ( A .ih 0h ) = 0 ) $=
    ( chil wcel c0v csp co ccj cfv cc0 wceq ax-hv0cl ax-his1 mpan2 hi01t
    fveq2d cj0 syl6eq eqtrd ) ABCZADEFZDAEFZGHZISDBCTUBJKADLMSUBIGHISUAIGANOPQR
    $.
    $( [13-Oct-1999] $)

  $( Inner product with self is not negative. $)
  hiidge0t $p |- ( A e. H~ -> 0 <_ ( A .ih A ) ) $=
    ( chil wcel cc0 csp co cle wbr clt wceq wo c0v wn pm2.1 ax-his4OLD ex wa
    opreq1 hi01t sylan9eqr eqcomd orim12d mpi cr wb hiidrclt 0re jctil leloet
    syl mpbird ) ABCZDAAEFZGHZDUMIHZDUMJZKZULALJZMZURKUQURNULUSUOURUPULUSUOAOPU
    LURUPULURQUMDURULUMLAEFDALAERASTUAPUBUCULDUDCZUMUDCZQUNUQUEULVAUTAUFUGUHDUM
    UIUJUK $.
    $( [29-May-1999] $)

  $( Zero inner product with self means vector is zero.  Lemma 3.1(S6) of
     [Beran] p. 95. $)
  his6t $p |- ( A e. H~ -> ( ( A .ih A ) = 0 <-> A = 0h ) ) $=
    ( chil wcel csp co cc0 wceq c0v wn wa clt wbr ax-his4OLD wb cr cle 0re
    leltnet mp3an1 hiidrclt hiidge0t sylanc adantr mpbid ex a3d eqcom syl5ib
    opreq1 eqeq1d hi01t syl5bir com12 impbid ) ABCZAADEZFGZAHGZUOFUPGZURUQUOURU
    SUOURIZUSIZUOUTJFUPKLZVAAMUOVBVANZUTUPOCZFUPPLZVCUOFOCVDVEVCQFUPRSATAUAUBUC
    UDUEUFUPFUGUHURUOUQURUQHADEZFGUOURUPVFFAHADUIUJAUKULUMUN $.
    $( [27-Jul-1999] $)

  ${
    his1.1 $e |- A e. H~ $.
    his1.2 $e |- B e. H~ $.
    $( Conjugate law for inner product.  Postulate (S1) of [Beran] p. 95. $)
    his1 $p |- ( A .ih B ) = ( * ` ( B .ih A ) ) $=
      ( chil wcel csp co ccj cfv wceq ax-his1 mp2an ) AEFBEFABGHBAGHIJKCDABLM
      $.
      $( [15-May-2005] $) $( [15-May-2005] $)
  $}

  $( Commuted inner products have the same absolute values. $)
  abshicomt $p |- ( ( A e. H~ /\ B e. H~ ) ->
                  ( abs ` ( A .ih B ) ) = ( abs ` ( B .ih A ) ) ) $=
    ( chil wcel wa csp co cabs cfv ccj ax-his1 fveq2d cc wceq ax-hicl ancoms
    abscjt syl eqtrd ) ACDZBCDZEZABFGZHIBAFGZJIZHIZUDHIZUBUCUEHABKLUBUDMDZUFUGN
    UATUHBAOPUDQRS $.
    $( [20-Jun-2006] $) $( [26-May-2006] $)

  ${
    $d x A $.
    $( A vector whose inner product is always zero is zero. $)
    hial0 $p |- ( A e. H~ -> ( A. x e. H~ ( A .ih x ) = 0 <-> A = 0h ) ) $=
      ( chil wcel cv csp co cc0 wceq wral c0v opreq2 eqeq1d rcla4v his6t
      sylibd wi opreq1 hi01t sylan9eq ex a1i r19.21adv impbid ) BCDZBAEZFGZHIZA
      CJZBKIZUEUIBBFGZHIZUJUHULABCUFBIUGUKHUFBBFLMNBOPUEUJUHACUJUFCDZUHQQUEUJUM
      UHUJUMUGKUFFGHBKUFFRUFSTUAUBUCUD $.
      $( [24-Oct-1999] $)

    $( A vector whose inner product is always zero is zero. $)
    hial02 $p |- ( A e. H~ -> ( A. x e. H~ ( x .ih A ) = 0 <-> A = 0h ) ) $=
      ( chil wcel cv csp co cc0 wceq wral c0v opreq1 eqeq1d rcla4v his6t
      sylibd wi opreq2 hi02t sylan9eq ex a1i r19.21adv impbid ) BCDZAEZBFGZHIZA
      CJZBKIZUEUIBBFGZHIZUJUHULABCUFBIUGUKHUFBBFLMNBOPUEUJUHACUJUFCDZUHQQUEUJUM
      UHUJUMUGUFKFGHBKUFFRUFSTUAUBUCUD $.
      $( [28-Jan-2006] $) $( [28-Jan-2006] $)
  $}

  ${
    hisubcom.1 $e |- A e. H~ $.
    hisubcom.2 $e |- B e. H~ $.
    hisubcom.3 $e |- C e. H~ $.
    hisubcom.4 $e |- D e. H~ $.
    $( Two vector subtractions simultaneously commute in an inner product. $)
    hisubcom $p |- ( ( A -h B ) .ih ( C -h D ) ) =
                   ( ( B -h A ) .ih ( D -h C ) ) $=
      ( c1 cneg cmv co csm csp hvnegdi opreq12i ccj cfv cmul ax1cn negcl
      hvsubcl his35 cr wcel wceq 1re renegcl cjret ax-mp opreq2i mul2neg
      mulid1 3eqtr opreq1i hicl mulid2 eqtr3 ) IJZBAKLZMLZUSDCKLZMLZNLZABKLZCDK
      LZNLUTVBNLZVAVEVCVFNBAFEODCHGOPVDUSUSQRZSLZVGSLIVGSLVGUSUSUTVBITUAZVJBAFE
      UBZDCHGUBZUCVIIVGSVIUSUSSLIISLIVHUSUSSUSUDUEVHUSUFIUGUHUSUIUJUKIITTULITUM
      UNUOVGUTVBVKVLUPUQUNUR $.
      $( [3-Jul-2005] $) $( [1-Jul-2005] $)
  $}

  $( Lemma used to prove equality of vectors. $)
  hi2eqt $p |- ( ( A e. H~ /\ B e. H~ ) ->
               ( ( A .ih ( A -h B ) ) = ( B .ih ( A -h B ) ) <-> A = B ) ) $=
    ( chil wcel wa cmv co csp cmin cc0 wceq c0v his2subt pm3.26 pm3.27
    hvsubclt syl3anc eqeq1d wb his6t syl bitr3d cc subeq0t ax-hicl syldan
    sylanc hvsubeq0t 3bitr3d ) ACDZBCDZEZAABFGZHGZBUMHGZIGZJKZUMLKZUNUOKZABKULU
    MUMHGZJKZUQURULUTUPJUJUKUMCDZUTUPKULABUMMUJUKNUJUKOZABPZQRULVBVAURSVDUMTUAU
    BUNUCDZUOUCDZUQUSSULUNUOUDUJUKVBVEVDAUMUEUFUKVBVFULBUMUEVCVDUGUGABUHUI $.
    $( [16-Nov-1999] $)

  ${
    $d x A $.  $d x B $.
    $( Two vectors whose inner product is always equal are equal. $)
    hial2eqt $p |- ( ( A e. H~ /\ B e. H~ ) ->
                   ( A. x e. H~ ( A .ih x ) = ( B .ih x ) <-> A = B ) ) $=
      ( chil wcel wa cv csp co wceq wral cmv wi hvsubclt opreq2 eqeq12d rcla4v
      syl hi2eqt sylibd opreq1 a1d r19.21aiv impbid1 ) BDECDEFZBAGZHIZCUFHIZJZA
      DKZBCJZUEUJBBCLIZHIZCULHIZJZUKUEULDEUJUOMBCNUIUOAULDUFULJUGUMUHUNUFULBHOU
      FULCHOPQRBCSTUKUIADUKUIUFDEBCUFHUAUBUCUD $.
      $( [16-Nov-1999] $)

    $( Two vectors whose inner product is always equal are equal. $)
    hial2eq2t $p |- ( ( A e. H~ /\ B e. H~ ) ->
                   ( A. x e. H~ ( x .ih A ) = ( x .ih B ) <-> A = B ) ) $=
      ( chil wcel wa cv csp co wceq wral wb ccj cfv ax-his1 eqeqan12d cc cj11t
      ax-hicl ancoms syl2an bitr2d anandirs ralbidva hial2eqt bitrd ) BDEZCDEZF
      ZAGZBHIZUJCHIZJZADKBUJHIZCUJHIZJZADKBCJUIUMUPADUGUHUJDEZUMUPLUGUQFZUHUQFZ
      FUPUKMNZULMNZJZUMURUSUNUTUOVABUJOCUJOPUKQEZULQEZVBUMLURUSUKULRUQUGVCUJBST
      UQUHVDUJCSTUAUBUCUDABCUEUF $.
      $( [2-Feb-2006] $) $( [28-Jan-2006] $)
  $}

  $( Orthogonality commutes. $)
  orthcom $p |- ( ( A e. H~ /\ B e. H~ ) ->
                ( ( A .ih B ) = 0 <-> ( B .ih A ) = 0 ) ) $=
    ( chil wcel wa csp co cc0 wceq ccj cfv ax-his1 ancoms eqeq1d fveq2 cj0
    syl6eq syl5bir impbid ) ACDZBCDZEZABFGZHIZBAFGZHIZUBUFUCJKZHIUDUBUEUGHUATUE
    UGIBALMNUDUGHJKZHUCHJOPQRUBUDUEJKZHIUFUBUCUIHABLNUFUIUHHUEHJOPQRS $.
    $( [10-Oct-1999] $)

  ${
    normlem1.1 $e |- S e. CC $.
    normlem1.2 $e |- F e. H~ $.
    normlem1.3 $e |- G e. H~ $.
    $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
       [Beran] p. 97. $)
    normlem0 $p |- ( ( F -h ( S .h G ) ) .ih
                         ( F -h ( S .h G ) ) ) =
                ( ( ( F .ih F ) + ( -u ( * ` S ) x. ( F .ih G ) ) ) +
  ( ( -u S x. ( G .ih F ) ) + ( ( S x. ( * ` S ) ) x. ( G .ih G ) ) ) ) $=
      ( csm co cmv csp cneg cva caddc ccj cfv cmul c1 hvmulcl hvsubval mulm1
      opreq1i ax1cn negcl hvmulass eqtr3 opreq2i eqtr4 opreq12i chil wcel wceq
      hvaddcl ax-his2 mp3an his7t cc his5t cjneg eqtr ax-his3 hicl cjcl mulcl
      adddi mulass mul2neg 3eqtr ) BACGHZIHZVIJHBAKZCGHZLHZVLJHZBVLJHZVKVLJHZMH
      ZBBJHZANOZKZBCJHZPHZMHZVJCBJHZPHZAVRPHZCCJHZPHZMHZMHVIVLVIVLJVIBQKZVHGHZL
      HVLBVHEACDFRSVKWJBLWIAPHZCGHVKWJWKVJCGADTUAWIACQUBUCDFUDUEUFUGZWLUHBUIUJZ
      VKUIUJZVLUIUJZVMVPUKEVJCADUCZFRZBVKEWQULZBVKVLUMUNVNWBVOWHMVNVQBVKJHZMHZW
      BWMWMWNVNWTUKEEWQBBVKUOUNWSWAVQMWSVJNOZVTPHZWAVJUPUJZWMCUIUJZWSXBUKWPEFVJ
      BCUQUNXAVSVTPADURZUAUSUFUSVOVJCVLJHZPHZVJWCXAWFPHZMHZPHZWHXCXDWOVOXGUKWPF
      WRVJCVLUTUNXFXIVJPXFWCCVKJHZMHZXIXDWMWNXFXLUKFEWQCBVKUOUNXKXHWCMXCXDXDXKX
      HUKWPFFVJCCUQUNUFUSUFXJWDVJXHPHZMHWHVJWCXHWPCBFEVAXAWFVJWPVBZCCFFVAZVCVDX
      MWGWDMVJXAPHZWFPHXMWGVJXAWFWPXNXOVEXPWEWFPXPVJVSPHWEXAVSVJPXEUFAVRDADVBVF
      USUAUEUFUSVGUHVG $.
      $( [7-Oct-1999] $)

    ${
      normlem1.4 $e |- R e. RR $.
      normlem1.5 $e |- ( abs ` S ) = 1 $.
      $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
         [Beran] p. 97. $)
      normlem1 $p |- ( ( F -h ( ( S x. R ) .h G ) ) .ih
                         ( F -h ( ( S x. R ) .h G ) ) ) =
                ( ( ( F .ih F ) + ( ( ( * ` S ) x. -u R ) x. ( F .ih G ) ) ) +
  ( ( ( S x. -u R ) x. ( G .ih F ) ) + ( ( R ^ 2 ) x. ( G .ih G ) ) ) ) $=
        ( cmul co csm cmv csp ccj cfv cneg caddc c2 cexp recn mulcl normlem0
        cjmul cr wcel wceq cjreb mpbi opreq2i eqtr negeqi cjcl mulneg2 eqtr4
        opreq1i eqcomi mul4 c1 cabs absvalsq sq1 3eqtr3 opreq12i mulid2 3eqtr
        sqval ) CBAJKZDLKMKZVINKCCNKZVHOPZQZCDNKZJKZRKZVHQZDCNKZJKZVHVKJKZDDNKZ
        JKZRKZRKVJBOPZAQZJKZVMJKZRKZBWDJKZVQJKZASTKZVTJKZRKZRKVHCDBAEAHUAZUBFGU
        CVOWGWBWLRVNWFVJRVLWEVMJVLWCAJKZQWEVKWNVKWCAOPZJKZWNBAEWMUDZWOAWCJAUEUF
        WOAUGHAWMUHUIZUJUKULWCABEUMZWMUNUOUPUJVRWIWAWKRVPWHVQJWHVPBAEWMUNUQUPVS
        WJVTJVSAAJKZWJVSVHWPJKBWCJKZAWOJKZJKZWTVKWPVHJWQUJBAWCWOEWMWSAWMUMURXCU
        SWTJKWTXAUSXBWTJBUTPZSTKUSSTKXAUSXDUSSTIUPBEVAVBVCWOAAJWRUJVDWTAAWMWMUB
        VEUKVFAWMVGUOUPVDVDUK $.
        $( [22-Aug-1999] $)
    $}

    ${
      normlem2.4 $e |- B = -u ( ( ( * ` S ) x. ( F .ih G ) ) +
                    ( S x. ( G .ih F ) ) ) $.
      $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
         [Beran] p. 97. $)
      normlem2 $p |- B e. RR $=
        ( ccj cfv csp co cmul caddc cneg cr wcel wceq cjcj eqcomi his1
        opreq12i cjcl hicl cjmul eqtr4 opreq2i mulcl addcom cjadd 3eqtr4r
        addcl cjreb mpbir renegcl eqeltr ) ABIJZCDKLZMLZBDCKLZMLZNLZOPHVBVBPQVB
        IJZVBRVAUSNLUSIJZVAIJZNLVBVCVAVDUSVENVAUQIJZURIJZMLVDBVFUTVGMVFBBESTDCG
        FUAUBUQURBEUCZCDFGUDZUEUFUSUQUTIJZMLVEURVJUQMCDFGUAUGBUTEDCGFUDZUEUFUBU
        SVAUQURVHVIUHZBUTEVKUHZUIUSVAVLVMUJUKVBUSVAVLVMULUMUNUOUP $.
        $( [27-Jul-1999] $)

      ${
        normlem3.5 $e |- A = ( G .ih G ) $.
        normlem3.6 $e |- C = ( F .ih F ) $.
        ${
          normlem3.7 $e |- R e. RR $.
          $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii)
             of [Beran] p. 97. $)
          normlem3 $p |- ( ( ( A x. ( R ^ 2 ) ) + ( B x. R ) ) + C ) =
                ( ( ( F .ih F ) + ( ( ( * ` S ) x. -u R ) x. ( F .ih G ) ) ) +
  ( ( ( S x. -u R ) x. ( G .ih F ) ) + ( ( R ^ 2 ) x. ( G .ih G ) ) ) ) $=
            ( c2 cexp co cmul caddc csp ccj cfv cneg cc hicl eqeltr recn sqcl
            mulcl normlem2 addcom cjcl addcl mulneg1 opreq1i mulneg2 3eqtr4
            negcl adddir mul23 opreq12i 3eqtr mulcom opreq2i eqtr3 addass ) CAD
            OPQZRQZBDRQZSQZSQFFTQZEUAUBZDUCZRQZFGTQZRQZEVMRQZGFTQZRQZVGGGTQZRQZ
            SQZSQZSQVJCSQVKVPSQWBSQCVKVJWCSMVJVIVHSQVPVSSQZWASQWCVHVIAVGAVTUDLG
            GJJUEZUFZDDNUGZUHZUIZBDBBEFGHIJKUJUGWGUIZUKVIWDVHWASVIVLVORQZEVRRQZ
            SQZVMRQZWKVMRQZWLVMRQZSQWDWMUCZDRQWMDRQUCVIWNWMDWKWLVLVOEHULZFGIJUE
            ZUIZEVRHGFJIUEZUIZUMZWGUNBWQDRKUOWMDXCWGUPUQWKWLVMWTXBDWGURZUSWOVPW
            PVSSVLVOVMWRWSXDUTEVRVMHXAXDUTVAVBVGARQVHWAVGAWHWFVCAVTVGRLVDVEVAVP
            VSWAVNVOVLVMWRXDUIWSUIZVQVREVMHXDUIXAUIZVGVTWHWEUIZVFVBVAVJCVHVIWIW
            JUMCVKUDMFFIIUEZUFUKVKVPWBXHXEVSWAXFXGUMVFUQ $.
            $( [21-Aug-1999] $)
        $}


        ${
          normlem4.7 $e |- R e. RR $.
          normlem4.8 $e |- ( abs ` S ) = 1 $.
          $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii)
             of [Beran] p. 97. $)
          normlem4 $p |- ( ( F -h ( ( S x. R ) .h G ) ) .ih
                       ( F -h ( ( S x. R ) .h G ) ) ) =
                       ( ( ( A x. ( R ^ 2 ) ) + ( B x. R ) ) + C ) $=
            ( cmul co csm cmv csp ccj cfv cneg caddc c2 cexp normlem1 normlem3
            eqtr4 ) FEDPQGRQSQZUJTQFFTQEUAUBDUCZPQFGTQPQUDQEUKPQGFTQPQDUEUFQZGG
            TQPQUDQUDQAULPQBDPQUDQCUDQDEFGHIJNOUGABCDEFGHIJKLMNUHUI $.
            $( [29-Jul-1999] $)

          $( Lemma used to derive properties of norm.  Part of Theorem
             3.3(ii) of [Beran] p. 97. $)
          normlem5 $p |- 0 <_
                       ( ( ( A x. ( R ^ 2 ) ) + ( B x. R ) ) + C ) $=
            ( cc0 cmul co csm cmv csp c2 cexp caddc cle chil wcel wbr recn
            mulcl hvmulcl hvsubcl hiidge0t ax-mp normlem4 breqtr ) PFEDQRZGSRZT
            RZUSUARZADUBUCRQRBDQRUDRCUDRUEUSUFUGPUTUEUHFURIUQGEDHDNUIUJJUKULUSU
            MUNABCDEFGHIJKLMNOUOUP $.
            $( [10-Aug-1999] $)
        $}

        ${
          $d x A $.  $d x B $.  $d x C $.
          normlem6.7 $e |- ( abs ` S ) = 1 $.
          $( Lemma used to derive properties of norm.  Part of Theorem
             3.3(ii) of [Beran] p. 97. $)
          normlem6 $p |- ( abs ` B ) <_
                       ( 2 x. ( ( sqr ` A ) x. ( sqr ` C ) ) ) $=
            ( vx c2 cexp co csqr cfv c4 cmul cabs cle wbr cc0 caddc cmin csp
            chil wcel hiidge0t ax-mp breqtrr cr hiidrclt eqeltr normlem2 cv
            cif wceq opreq1 opreq2d opreq2 opreq12d opreq1d breq2d 0re elimel
            normlem5 dedth rgen discrlem resqcl 4re remulcl lesubadd2 mpbi
            recn addid1 breqtr wb sqge0 4pos ltlei mulge0 mp2an sqrle absre
            sqrmuli sqr4 opreq12i eqtr2 3brtr4 ) BOPQZRSZTACUAQZUAQZRSZBUBSOARS
            CRSUAQZUAQZUCWNWQUCUDZWOWRUCUDZWNWQUEUFQZWQUCWNWQUGQUEUCUDZWNXCUCUD
            UEAUCUDZXDUEFFUHQZAUCFUIUJZUEXFUCUDIFUKULKUMZNABCAXFUNKXGXFUNUJIFUO
            ULUPZBDEFGHIJUQZCEEUHQZUNLEUIUJZXKUNUJHEUOULUPZUEANURZOPQZUAQZBXNUA
            QZUFQZCUFQZUCUDZNUNXNUNUJZXTUEAYAXNUEUSZOPQZUAQZBYBUAQZUFQZCUFQZUCU
            DXNUEXNYBUTZXSYGUEUCYHXRYFCUFYHXPYDXQYEUFYHXOYCAUAXNYBOPVAVBXNYBBUA
            VCVDVEVFABCYBDEFGHIJKLXNUEUNVGVHMVIVJVKVLULWNWQUEBXJVMZTWPVNACXIXMV
            OZVOZVGVPVQWQWQYKVRVSVTUEWNUCUDUEWQUCUDZXAXBWABXJWBUETUCUDUEWPUCUDZ
            YLUETVGVNWCWDZXEUECUCUDYMXHUEXKCUCXLUEXKUCUDHEUKULLUMZACXIXMWEWFZTW
            PVNYJWEWFWNWQYIYKWGWFVQBXJWHWRTRSZWPRSZUAQWTTWPVNYJYNYPWIYQOYRWSUAW
            JACXIXMXHYOWIWKWLWM $.
            $( [2-Aug-1999] $)

        $}
      $}
    $}

    ${
      normlem7.4 $e |- ( abs ` S ) = 1 $.
      $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
         [Beran] p. 97. $)
      normlem7 $p |- ( ( ( * ` S ) x. ( F .ih G ) ) + ( S x. ( G .ih F ) ) ) <_
                ( 2 x. ( ( sqr ` ( G .ih G ) ) x. ( sqr ` ( F .ih F ) ) ) ) $=
        ( ccj cfv csp co cmul caddc cneg cabs cle wbr c2 csqr cr wcel eqid
        normlem2 cjcl hicl mulcl addcl negreb mpbi leabs absneg breqtrr
        normlem6 negcl abscl 2re cc0 chil hiidge0t ax-mp hiidrclt sqrcl
        remulcl letr mp2an ) AHIZBCJKZLKZACBJKZLKZMKZVKNZOIZPQVMRCCJKZSIZBBJKZS
        IZLKZLKZPQVKVSPQVKVKOIVMPVKVLTUAVKTUAVLABCDEFVLUBZUCVKVHVJVFVGADUDBCEFU
        EUFAVIDCBFEUEUFUGZUHUIZUJVKWAUKULVNVLVPABCDEFVTVNUBVPUBGUMVKVMVSWBVLVKW
        AUNUORVRUPVOVQUQVNPQZVOTUACURUAZWCFCUSUTVNWDVNTUAFCVAUTVBUTUQVPPQZVQTUA
        BURUAZWEEBUSUTVPWFVPTUAEBVAUTVBUTVCVCVDVE $.
        $( [11-Aug-1999] $)
    $}
  $}

  ${
    normlem8.1 $e |- A e. H~ $.
    normlem8.2 $e |- B e. H~ $.
    normlem8.3 $e |- C e. H~ $.
    normlem8.4 $e |- D e. H~ $.
    $( Lemma used to derive properties of norm. $)
    normlem8 $p |- ( ( A +h B ) .ih ( C +h D ) ) =
        ( ( ( A .ih C ) + ( B .ih D ) ) + ( ( A .ih D ) + ( B .ih C ) ) ) $=
      ( cva co csp caddc chil wcel wceq his7t mp3an opreq12i hvaddcl ax-his2
      hicl add42 3eqtr4 ) ACDIJZKJZBUDKJZLJZACKJZADKJZLJZBCKJZBDKJZLJZLJABIJUDK
      JZUHULLJUIUKLJLJUEUJUFUMLAMNZCMNZDMNZUEUJOEGHACDPQBMNZUPUQUFUMOFGHBCDPQRU
      OURUDMNUNUGOEFCDGHSABUDTQUHULUIUKACEGUABDFHUAADEHUABCFGUAUBUC $.
      $( [1-Jul-2005] $) $( [30-Jun-2005] $)

    $( Lemma used to derive properties of norm. $)
    normlem9 $p |- ( ( A -h B ) .ih ( C -h D ) ) =
        ( ( ( A .ih C ) + ( B .ih D ) ) - ( ( A .ih D ) + ( B .ih C ) ) ) $=
      ( cmv co csp c1 cneg csm cva caddc cmin hvsubval opreq12i ax1cn negcl
      hvmulcl normlem8 cmul ccj cfv cc wcel chil wceq ax-his3 mp3an his5t
      opreq2i cr 1re renegcl cjret ax-mp mul2neg mulid2 3eqtr opreq1i cjcl
      hicl mulass 3eqtr3 mulm1 eqtr negdi eqtr4 addcl negsub ) ABIJZCDIJZKJALMZ
      BNJZOJZCVPDNJZOJZKJACKJZVQVSKJZPJZAVSKJZVQCKJZPJZPJZWABDKJZPJZADKJZBCKJZP
      JZQJZVNVRVOVTKABEFRCDGHRSAVQCVSEVPBLTUAZFUBGVPDWNHUBZUCWGWIWLMZPJWMWCWIWF
      WPPWBWHWAPWBVPBVSKJZUDJZVPVPUEUFZWHUDJZUDJZWHVPUGUHZBUIUHZVSUIUHWBWRUJWNF
      WOVPBVSUKULWQWTVPUDXBXCDUIUHZWQWTUJWNFHVPBDUMULUNVPWSUDJZWHUDJLWHUDJXAWHX
      ELWHUDXEVPVPUDJLLUDJLWSVPVPUDVPUOUHWSVPUJLUPUQVPURUSZUNLLTTUTLTVAVBVCVPWS
      WHWNVPWNVDBDFHVEZVFWHXGVAVGVBUNWFWJMZWKMZPJWPWDXHWEXIPWDWSWJUDJZVPWJUDJXH
      XBAUIUHXDWDXJUJWNEHVPADUMULWSVPWJUDXFVCWJADEHVEZVHVBWEVPWKUDJZXIXBXCCUIUH
      WEXLUJWNFGVPBCUKULWKBCFGVEZVHVISWJWKXKXMVJVKSWIWLWAWHACEGVEXGVLWJWKXKXMVL
      VMVIVB $.
      $( [1-Jul-2005] $) $( [30-Jun-2005] $)
  $}

  ${
    normlem7t.1 $e |- A e. H~ $.
    normlem7t.2 $e |- B e. H~ $.
    $( Lemma used to derive properties of norm.  Part of Theorem 3.3(ii) of
       [Beran] p. 97. $)
    normlem7t $p |- ( ( S e. CC /\ ( abs ` S ) = 1 ) ->
              ( ( ( * ` S ) x. ( A .ih B ) ) + ( S x. ( B .ih A ) ) ) <_
              ( 2 x. ( ( sqr ` ( B .ih B ) ) x. ( sqr ` ( A .ih A ) ) ) ) ) $=
      ( cc wcel cabs cfv c1 wceq wa ccj csp co cmul caddc c2 csqr cle wbr cif
      fveq2 opreq1d opreq1 opreq12d breq1d eleq1 eqeq1d anbi12d ax1cn cc0 0re
      1re lt01 ltlei absid ax-mp pm3.2i elimhyp pm3.26i pm3.27i normlem7 dedth
      ) CFGZCHIZJKZLZCMIZABNOZPOZCBANOZPOZQOZRBBNOSIAANOSIPOPOZTUAVHCJUBZMIZVJP
      OZVPVLPOZQOZVOTUACJCVPKZVNVTVOTWAVKVRVMVSQWAVIVQVJPCVPMUCUDCVPVLPUEUFUGVP
      ABVPFGZVPHIZJKZVHWBWDLJFGZJHIZJKZLCJWAVEWBVGWDCVPFUHWAVFWCJCVPHUCUIUJJVPK
      ZWEWBWGWDJVPFUHWHWFWCJJVPHUCUIUJWEWGUKULJTUAWGULJUMUNUOUPJUNUQURUSUTZVADE
      WBWDWIVBVCVD $.
      $( [11-Oct-1999] $)

    $( Equality case of Bunjakovaskij-Cauchy-Schwarz inequality.  Specifically,
       in the equality case the two vectors are collinear.  Compare ~ bcs . $)
    bcseq $p |- ( ( ( A .ih B ) x. ( B .ih A ) ) = ( ( A .ih A ) x.
             ( B .ih B ) ) <-> ( ( B .ih B ) .h A ) = ( ( A .ih B ) .h B ) ) $=
      ( csp co cmul wceq csm cmv c0v cc0 caddc cmin opreq1 eqcomd ccj cfv cc
      wcel chil hicl hvmulcl his5t mp3an cr hiidrclt ax-mp cjret ax-his3
      opreq12i mulcl mulcom opreq1i eqtr4 3eqtr his1 eqcomi mulass 3eqtr4g
      mul23 3eqtr4r a1i opreq12d opreq1d addcl subid syl6eq normlem9 syl5eq wb
      hvsubcl his6t sylib hvsubeq0 impbi ) ABEFZBAEFZGFZAAEFZBBEFZGFZHZWAAIFZVQ
      BIFZHZWCWDWEJFZKHZWFWCWGWGEFZLHZWHWCWDWDEFZWEWEEFZMFZWDWEEFZWEWDEFZMFZNFZ
      LWIWCWQWPWPNFLWCWMWPWPNWCWKWNWLWOMWCWBWAGFZVSWAGFZWKWNWCWSWRVSWBWAGOPWKWA
      QRZWDAEFZGFZWAWAVTGFZGFZWRWASTZWDUATZAUATZWKXBHBBDDUBZWAAXHCUCZCWAWDAUDUE
      WTWAXAXCGWAUFTZWTWAHBUATZXJDBUGUHWAUIUHZXEXGXGXAXCHXHCCWAAAUJUEZUKXDXCWAG
      FWRWAXCXHWAVTXHAACCUBZULUMWBXCWAGVTWAXNXHUMZUNUOUPWNVQQRZWDBEFZGFZVRWAVQG
      FZGFZWSVQSTZXFXKWNXRHABCDUBZXIDVQWDBUDUEXPVRXQXSGVRXPBADCUQURZXEXGXKXQXSH
      XHCDWAABUJUEUKXTXSVRGFWAVSGFZWSVRXSBADCUBZWAVQXHYBULUMWAVQVRXHYBYEUSWAVSX
      HVQVRYBYEULZUMUPUPUTWLWOHWCWTWEAEFZGFZYDWOWLWTWAYGVSGXLYAXKXGYGVSHYBDCVQB
      AUJUEZUKXEWEUATZXGWOYHHXHVQBYBDUCZCWAWEAUDUEWLXPWEBEFZGFZVRVQWAGFZGFZYDYA
      YJXKWLYMHYBYKDVQWEBUDUEXPVRYLYNGYCYAXKXKYLYNHYBDDVQBBUJUEUKYOYNVRGFWSYDVR
      YNYEVQWAYBXHULUMVQWAVRYBXHYEVAVSWAYFXHUMUPUPVBVCVDVEWPWNWOWDWEXIYKUBWEWDY
      KXIUBVFVGVHWDWEWDWEXIYKXIYKVIVJWGUATWJWHVKWDWEXIYKVLWGVMUHVNWDWEXIYKVOVNW
      FWBVSWFXAYGWBVSWDWEAEOWBXCXAXOXMUOYGVSYIURUTPVP $.
      $( [16-Jul-2001] $)
  $}

  $( Lemma used to derive properties of norm.  Part of Remark 3.4(B) of
     [Beran] p. 98. $)
  normlem9at $p |- ( ( A e. H~ /\ B e. H~ ) -> ( ( A -h B ) .ih ( A -h B ) ) =
      ( ( ( A .ih A ) + ( B .ih B ) ) - ( ( A .ih B ) + ( B .ih A ) ) ) ) $=
    ( chil wcel cmv co csp caddc cmin wceq c0v cif opreq1 opreq12d id opreq1d
    opreq2 eqeq12d opreq2d ax-hv0cl elimel normlem9 dedth2h ) ACDZBCDZABEFZUFGF
    ZAAGFZBBGFZHFZABGFZBAGFZHFZIFZJUDAKLZBEFZUPGFZUOUOGFZUIHFZUOBGFZBUOGFZHFZIF
    ZJUOUEBKLZEFZVEGFZURVDVDGFZHFZUOVDGFZVDUOGFZHFZIFZJABKKAUOJZUGUQUNVCVMUFUPU
    FUPGAUOBEMZVNNVMUJUSUMVBIVMUHURUIHVMAUOAUOGVMOZVONPVMUKUTULVAHAUOBGMAUOBGQN
    NRBVDJZUQVFVCVLVPUPVEUPVEGBVDUOEQZVQNVPUSVHVBVKIVPUIVGURHVPBVDBVDGVPOZVRNSV
    PUTVIVAVJHBVDUOGQBVDUOGMNNRUOVDUOVDAKCTUAZBKCTUAZVSVTUBUC $.
    $( [30-May-2005] $) $( [10-May-2005] $)

