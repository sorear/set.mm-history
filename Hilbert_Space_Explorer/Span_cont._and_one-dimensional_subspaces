$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Span (cont.) and one-dimensional subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The span of the singleton of the zero vector is the zero subspace. $)
  spansn0 $p |- ( span ` { 0h } ) = 0H $=
    ( c0h cspn cfv c0v csn df-ch0 fveq2i csh wcel wceq h0elsh spanid ax-mp
    eqtr3i ) ABCZDEZBCAAPBFGAHIOAJKALMN $.
    $( [14-Jan-2005] $)

  $( The span of the empty set is the zero subspace.  Remark 11.6.e of
     [Schechter] p. 276. $)
  span0 $p |- ( span ` (/) ) = 0H $=
    ( c0 cspn cfv c0h chil wss h0elsh shssii 0ss spanss mp2an csh wcel wceq
    spanid ax-mp sseqtri spancl sh0le eqssi ) ABCZDUADBCZDDEFADFUAUBFDGHDIADJKD
    LMUBDNGDOPQUALMZDUAFAEFUCEIARPUASPT $.
    $( [3-Jun-2004] $)

  ${
    $d x A $.  $d x B $.
    elspan.1 $e |- B e. _V $.
    $( Membership in the span of a subset of Hilbert space. $)
    elspani $p |- ( A C_ ~H ->
                ( B e. ( span ` A ) <-> A. x e. SH ( A C_ x -> B e. x ) ) ) $=
      ( chil wss cspn cfv wcel cv csh crab cint wi wral spanval eleq2d
      elintrab syl6bb ) BEFZCBGHZICBAJZFZAKLMZIUCCUBINAKOTUAUDCABPQUCACKDRS $.
      $( [2-Jun-2004] $)

  $}

  ${
    spanun.1 $e |- A C_ ~H $.
    spanun.2 $e |- B C_ ~H $.
    $d x y z w A $.  $d x y z w B $.
    $( The span of a union is the subspace sum of spans. $)
    spanuni $p |- ( span ` ( A u. B ) ) = ( ( span ` A ) +H ( span ` B ) ) $=
      ( vx vz vw vy cun cspn cfv cph co chil wss csh wcel spancl ax-mp shscli
      shssii spanss2 unss12 mp2an shunssi sstri spanss wceq spanid sseqtri cv
      wa cva wex wrex shseli r2ex bitri wi wral r19.27av wb visset elspani
      anbi12i r19.26 bitr4i sylanb prth unss syl5ibr shaddclOLD sylan9r eleq1
      biimprd sylan9 expl ralimia unssi sylibr syl 19.23aivv sylbi ssriv eqssi
      ) ABIZJKZAJKZBJKZLMZWGWJJKZWJWJNOWFWJOWGWKOWJWHWIANOZWHPQCARSZBNOZWIPQDBR
      SZTZUAWFWHWIIZWJAWHOZBWIOZWFWQOWLWRCAUBSWNWSDBUBSAWHBWIUCUDWHWIWMWOUEUFWF
      WJUGUDWJPQWKWJUHWPWJUISUJEWJWGEUKZWJQZFUKZWHQZGUKZWIQZULZWTXBXDUMMZUHZULZ
      GUNFUNZWTWGQZXAXHGWIUOFWHUOXJFGWHWIWTWMWOUPXHFGWHWIUQURXIXKFGXIAHUKZOZXBX
      LQZUSZBXLOZXDXLQZUSZULZXHULZHPUTZXKXSHPUTZXHYAXFXSXHHPVAXFXOHPUTZXRHPUTZU
      LYBXCYCXEYDWLXCYCVBCHAXBFVCVDSWNXEYDVBDHBXDGVCVDSVEXOXRHPVFVGVHYAWFXLOZWT
      XLQZUSZHPUTZXKXTYGHPXLPQZXSXHYGYIXSULYEXGXLQZXHYFXSYEXNXQULZYIYJXSXMXPULY
      KYEXMXNXPXQVIABXLVJVKXBXDXLVLVMXHYFYJWTXGXLVNVOVPVQVRWFNOXKYHVBABNCDVSHWF
      WTEVCVDSVTWAWBWCWDWE $.
      $( [2-Jun-2004] $)
  $}

  $( The span of a union is the subspace sum of spans. $)
  spanun $p |- ( ( A C_ ~H /\ B C_ ~H ) ->
                ( span ` ( A u. B ) ) = ( ( span ` A ) +H ( span ` B ) ) ) $=
    ( chil wss cun cspn cfv cph co wceq cif uneq1 fveq2d fveq2 opreq1d eqeq12d
    uneq2 opreq2d sseq1 ssid elimhyp spanuni dedth2h ) ACDZBCDZABEZFGZAFGZBFGZH
    IZJUDACKZBEZFGZUKFGZUIHIZJUKUEBCKZEZFGZUNUPFGZHIZJABCCAUKJZUGUMUJUOVAUFULFA
    UKBLMVAUHUNUIHAUKFNOPBUPJZUMURUOUTVBULUQFBUPUKQMVBUIUSUNHBUPFNRPUKUPUDUKCDC
    CDZACAUKCSCUKCSCTZUAUEUPCDVCBCBUPCSCUPCSVDUAUBUC $.
    $( [9-Jun-2006] $)

  ${
    sshjococ.1 $e |- A C_ ~H $.
    sshjococ.2 $e |- B C_ ~H $.
    $( The join of two Hilbert space subsets (not necessarily closed subspaces)
       equals the join of their closures (double orthocomplements). $)
    sshhococi $p |- ( A vH B ) =
                 ( ( _|_ ` ( _|_ ` A ) ) vH ( _|_ ` ( _|_ ` B ) ) ) $=
      ( chj co cort cfv cun wss chil ococss ax-mp unss12 mp2an unssi occli
      choccli chssii occon2i wceq sshjval chjvali 3sstr4i ssun1 sstri sseqtr4i
      cch wcel sshjcl ssun2 chlubii ococi sseqtri eqssi ) ABEFZAGHZGHZBGHZGHZEF
      ZABIZGHGHZURUTIZGHGHZUPVAVBVDJZVCVEJAURJZBUTJZVFAKJZVGCALMBKJZVHDBLMAURBU
      TNOVBVDABKCDPZURUTKURUQACQRZSUTUSBDQRZSPTMVIVJUPVCUACDABUBOZURUTVLVMUCUDV
      AUPGHZGHZUPURVPJZUTVPJZVAVPJAUPJVQAVCUPAVBVCABUEVBKJVBVCJVKVBLMZUFVNUGAUP
      CUPVIVJUPUHUICDABUJOZSZTMBUPJVRBVCUPBVBVCBAUKVSUFVNUGBUPDWATMURUTVPVLVMVO
      UPVTRRULOUPVTUMUNUO $.
      $( [1-Jun-2004] $)
  $}

  $( Hilbert space has a nonzero vector iff it is not trivial. $)
  hne0 $p |- ( ~H =/= 0H <-> E. x e. ~H x =/= 0h ) $=
    ( chil helch chne0i ) ABCD $.
    $( [24-Feb-2006] $)

  $( The supremum of the empty set. $)
  chsup0 $p |- ( \/H ` (/) ) = 0H $=
    ( c0 chsup cfv c0h wss wceq csn 0ss cch wi wcel h0elch snssi ax-mp chsupss
    mp2an chsupsn sseqtri chsupcl chle0i mpbi ) ABCZDEUBDFUBDGZBCZDAUCEZUBUDEZU
    CHAIEZUCIEZUEUFJIHZDIKZUHLDIMNAUCOPNUJUDDFLDQNRUBUGUBIKUIASNTUA $.
    $( [13-Aug-2002] $)

  ${
    $d x A $.  $d x B $.
    h1deot.1 $e |- B e. ~H $.
    $( Membership in orthocomplement of 1-dimensional subspace. $)
    h1deoi $p |- ( A e. ( _|_ ` { B } ) <->
                 ( A e. ~H /\ ( A .ih B ) = 0 ) ) $=
      ( vx csn cort cfv wcel chil cv csp co cc0 wceq wral wa wss wb snssi
      ax-mp ocel wi wal df-ral elsn imbi1i albii elisseti opreq2 eqeq1d
      ceqsalv 3bitri anbi2i bitri ) ABEZFGHZAIHZADJZKLZMNZDUOOZPZUQABKLZMNZPUOI
      QZUPVBRBIHVECBISTDAUOUATVAVDUQVAURUOHZUTUBZDUCURBNZUTUBZDUCVDUTDUOUDVGVID
      VFVHUTDBUEUFUGUTVDDBBICUHVHUSVCMURBAKUIUJUKULUMUN $.
      $( [7-Jul-2001] $)

    $( Membership in 1-dimensional subspace. $)
    h1dei $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
    ( A e. ~H /\ A. x e. ~H ( ( B .ih x ) = 0 -> ( A .ih x ) = 0 ) ) ) $=
      ( csn cort cfv wcel chil cv csp co cc0 wceq wral wa wi wss wb snssi
      ax-mp occli chssii ocel h1deoi orthcom mpan2 pm5.32i bitri imbi1i impexp
      ralbii2 anbi2i ) BCEZFGZFGHZBIHZBAJZKLMNZAUOOZPZUQCURKLMNZUSQZAIOZPUOIRUP
      VASUOUNCIHZUNIRDCITUAUBUCABUOUDUAUTVDUQUSVCAUOIURUOHZUSQURIHZVBPZUSQVGVCQ
      VFVHUSVFVGURCKLMNZPVHURCDUEVGVIVBVGVEVIVBSDURCUFUGUHUIUJVGVBUSUKUIULUMUI
      $.
      $( [7-Jul-2001] $)
  $}

  $( A generating vector belongs to the 1-dimensional subspace it generates. $)
  h1did $p |- ( A e. ~H -> A e. ( _|_ ` ( _|_ ` { A } ) ) ) $=
    ( chil wcel csn cort cfv wss snssi ococss syl snssg mpbird ) ABCZAADZEFEFZC
    NOGZMNBGPABHNIJAOBKL $.
    $( [22-Jul-2001] $)

  $( A nonzero vector generates a (nonzero) 1-dimensional subspace. $)
  h1dn0 $p |- ( ( A e. ~H /\ A =/= 0h ) ->
              ( _|_ ` ( _|_ ` { A } ) ) =/= 0H ) $=
    ( chil wcel c0v wne csn cort cfv c0h wceq eleq2 h1did syl5cbi elch0 syl6ib
    necon3d imp ) ABCZADEAFGHGHZIERSIADRSIJZAICZADJTASCUARSIAKALMANOPQ $.
    $( [19-Jun-2006] $) $( [22-Jul-2001] $)

  ${
    $d x A $.  $d x B $.
    h1de2.1 $e |- A e. ~H $.
    h1de2.2 $e |- B e. ~H $.
    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2i $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) ->
                ( ( B .ih B ) .h A ) = ( ( A .ih B ) .h B ) ) $=
      ( vx csn cort cfv wcel csp co cmul wceq csm cmin cc0 cmv chil hicli
      hvmulcli his2sub mp3an mulcomi cc ax-his3 3eqtr4i subeq0i mpbir eqtri cv
      wi wral h1dei mpbiran hvsubcli opreq2 eqeq1d imbi12d rcla4v ax-mp sylbi
      wb orthcom mp2an 3imtr4g mpi opreq12i eqtr2i syl5eq mulcli sylib eqcomd
      bcseqi ) ABFGHGHIZABJKZBAJKZLKZAAJKZBBJKZLKZMVSANKZVOBNKZMVNVTVQVNVTVQOKZ
      PMVTVQMVNWAWBQKZAJKZPWCVNWDBJKZPMZWEPMZWFWABJKZWBBJKZOKZPWARIZWBRIZBRIZWF
      WKMVSABBDDSZCTZVOBABCDSZDTZDWAWBBUAUBWKPMWIWJMVSVOLKZVOVSLKZWIWJVSVOWOWQU
      CVSUDIZARIZWNWIWSMWOCDVSABUEUBVOUDIZWNWNWJWTMWQDDVOBBUEUBUFWIWJWABWPDSWBB
      WRDSUGUHUIVNBWDJKZPMZAWDJKZPMZWGWHVNBEUJZJKZPMZAXHJKZPMZUKZERULZXEXGUKZVN
      XBXNEABDUMCUNWDRIZXNXOUKWAWBWPWRUOZXMXOEWDRXHWDMZXJXEXLXGXRXIXDPXHWDBJUPU
      QXRXKXFPXHWDAJUPUQURUSUTVAXPWNWGXEVBXQDWDBVCVDXPXBWHXGVBXQCWDAVCVDVEVFWEW
      AAJKZWBAJKZOKZWCWLWMXBWEYAMWPWRCWAWBAUAUBXSVTXTVQOXSVSVRLKZVTXAXBXBXSYBMW
      OCCVSAAUEUBVSVRWOAACCSZUCUIXCWNXBXTVQMWQDCVOBAUEUBVGVHVIVTVQVRVSYCWOVJVOV
      PWQBADCSVJUGVKVLABCDVMVK $.
      $( [17-Jul-2001] $)

    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2bi $p |- ( B =/= 0h -> ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                A = ( ( ( A .ih B ) / ( B .ih B ) ) .h B ) ) ) $=
      ( c0v wne csp co cc0 csn cort cfv wcel cdiv csm wceq wb chil his6 ax-mp
      necon3bii wa c1 h1de2i adantl opreq2d cmul cc hicli recclzi ax-hvmulass
      mp3an23 syl ax1cn divcan1zi opreq1d eqtr3d ax-hvmulid syl6eq adantr
      mulcom mpan2 divreczi eqtr4d 3eqtr3d ex eleq1 divclzi h1did csh wi wss
      snssi occli choccli chshii shmulclOLD syl5cbir impbid sylbir ) BEFBBGHZIF
      ZABJZKLZKLZMZAABGHZWANHZBOHZPZQWAIBEBRMZWAIPBEPQDBSTUAWBWFWJWBWFWJWBWFUBZ
      UCWANHZWAAOHZOHZWMWGBOHZOHZAWIWLWNWPWMOWFWNWPPWBABCDUDUEUFWBWOAPWFWBWOUCA
      OHZAWBWMWAUGHZAOHZWOWRWBWMUHMZWTWOPZWABBDDUIZUJZXAWAUHMARMZXBXCCWMWAAUKUL
      UMWBWSUCAOUCWAUNXCUOUPUQXEWRAPCAURTUSUTWBWQWIPWFWBWMWGUGHZBOHZWQWIWBXAXGW
      QPZXDXAWGUHMZWKXHABCDUIZDWMWGBUKULUMWBXFWHBOWBXFWGWMUGHZWHWBXAXFXKPZXDXAX
      IXLXJWMWGVAVBUMWGWAXJXCVCVDUPUQUTVEVFWJWFWIWEMZWBAWIWEVGWBWHUHMZXMWGWAXJX
      CVHXNBWEMZXMWKXODBVITWEVJMXNXOUBXMVKWEWDWCWKWCRVLDBRVMTVNVOVPWHBWEVQTVBUM
      VRVSVT $.
      $( [19-Jul-2001] $)

    $( Lemma for ~ h1de2ci . $)
    h1de2ctlem $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                 E. x e. CC A = ( x .h B ) ) $=
      ( csn cort cfv wcel cv csm co wceq cc wrex wi c0v cc0 sneq fveq2d eleq2d
      chil elisseti elsnc hsn0elch ococi eleq2i ax-hvmul0 ax-mp eqeq2i
      3bitr4ri syl6rbbr 0cn opreq1 eqeq2d rcla4ev mpan syl6bir wne csp cdiv
      h1de2bi wb his6 necon3bii hicli divclzi sylbir sylan ex sylbid pm2.61ine
      eleq1 h1did csh wa wss snssi occli choccli chshii shmulclOLD mpan2
      syl5cbir r19.23aiv impbii ) BCFZGHZGHZIZBAJZCKLZMZANOZWJWNPCQCQMZWJBRCKLZ
      MZWNWOWJBQFZGHZGHZIZWQWOWIWTBWOWHWSGWOWGWRGCQSTTUABWRIBQMXAWQBQBUBDUCUDWT
      WRBWRUEUFUGWPQBCUBIZWPQMECUHUIUJUKULRNIWQWNUMWMWQARNWKRMWLWPBWKRCKUNUOUPU
      QURCQUSZWJBBCUTLZCCUTLZVALZCKLZMZWNBCDEVBXCXHWNXFNIZXHWNXCWMXHAXFNWKXFMWL
      XGBWKXFCKUNUOUPXCXERUSXIXERCQXBXERMWOVCECVDUIVEXDXEBCDEVFCCEEVFVGVHVIVJVK
      VLWMWJANWMWJWLWIIZWKNIZBWLWIVMXKCWIIZXJXBXLECVNUIWIVOIXKXLVPXJPWIWHWGXBWG
      UBVQECUBVRUIVSVTWAWKCWIWBUIWCWDWEWF $.
      $( [10-Aug-2008] $) $( [19-Jul-2001] $)
  $}

  ${
    $d x A $.  $d x B $.
    h1de2ct.1 $e |- B e. ~H $.
    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2ci $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                 E. x e. CC A = ( x .h B ) ) $=
      ( csn cort cfv wcel chil cv csm co wceq cc wrex wss snssi ax-mp occli
      choccli cheli eleq1 hvmulcl mpan2 syl5cbir r19.23aiv wb c0v cif eqeq1
      rexbidv bibi12d ax-hv0cl elimel h1de2ctlem dedth pm5.21nii ) BCEZFGZFGZHZ
      BIHZBAJZCKLZMZANOZBUTUSURCIHZURIPDCIQRSTUAVEVBANVEVBVDIHZVCNHZBVDIUBVIVGV
      HDVCCUCUDUEUFVBVAVFUGVBBUHUIZUTHZVJVDMZANOZUGBUHBVJMZVAVKVFVMBVJUTUBVNVEV
      LANBVJVDUJUKULAVJCBUHIUMUNDUOUPUQ $.
      $( [21-Jul-2001] $)
  $}

  ${
    $d x y z A $.  $d x B $.
    spansn.1 $e |- A e. ~H $.
    $( The span of a singleton in Hilbert space equals its closure. $)
    spansni $p |- ( span ` { A } ) = ( _|_ ` ( _|_ ` { A } ) ) $=
      ( vx vy vz csn cspn cfv cort chil wss wcel snssi ax-mp spanssoc cv wi
      csh wral wa csm co wceq cc wrex h1de2ci shmulclOLD eleq1a com12 syl9r
      exp4a com3r r19.23aiv sylbi imp elisseti snss syl5ibr r19.21aiva wb
      visset elspani sylibr ssriv eqssi ) AFZGHZVFIHIHZVFJKZVGVHKAJLVIBAJMNZVFO
      NCVHVGCPZVHLZVFDPZKZVKVMLZQZDRSZVKVGLZVLVPDRVLVMRLZTAVMLZVOVNVLVSVTVOQZVL
      VKEPZAUAUBZUCZEUDUEVSWAQZEVKABUFWDWEEUDWDVSWBUDLZWAWDVSWFVTVOVSWFVTTWCVML
      ZWDVOWBAVMUGWGWDVOWCVMVKUHUIUJUKULUMUNUOAVMAJBUPUQURUSVIVRVQUTVJDVFVKCVAV
      BNVCVDVE $.
      $( [3-Jun-2004] $)

    $( Membership in the span of a singleton. $)
    elspansni $p |- ( B e. ( span ` { A } ) <-> E. x e. CC B = ( x .h A ) ) $=
      ( csn cspn cfv wcel cort cv csm co wceq cc wrex spansni eleq2i h1de2ci
      bitri ) CBEZFGZHCTIGIGZHCAJBKLMANOUAUBCBDPQACBDRS $.
      $( [3-Jun-2004] $)
  $}

  $( The span of a singleton in Hilbert space equals its closure. $)
  spansn $p |- ( A e. ~H -> ( span ` { A } ) = ( _|_ ` ( _|_ ` { A } ) ) ) $=
    ( chil wcel csn cspn cfv cort wceq c0v cif sneq fveq2d eqeq12d ax-hv0cl
    elimel spansni dedth ) ABCZADZEFZSGFZGFZHRAIJZDZEFZUDGFZGFZHAIAUCHZTUEUBUGU
    HSUDEAUCKZLUHUAUFGUHSUDGUILLMUCAIBNOPQ $.
    $( [4-Jun-2004] $)

  $( The span of a Hilbert space singleton belongs to the Hilbert lattice. $)
  spansnch $p |- ( A e. ~H -> ( span ` { A } ) e. CH ) $=
    ( chil wcel csn cspn cfv cort cch spansn wss snssi occl choccl 3syl
    eqeltrd ) ABCZADZEFQGFZGFZHAIPQBJRHCSHCABKQLRMNO $.
    $( [9-Jun-2004] $)

  $( The span of a Hilbert space singleton is a subspace. $)
  spansnsh $p |- ( A e. ~H -> ( span ` { A } ) e. SH ) $=
    ( chil wcel csn cspn cfv cch csh spansnch chsh syl ) ABCADEFZGCLHCAILJK $.
    $( [17-Dec-2004] $)

  ${
    spansnch.1 $e |- A e. ~H $.
    $( The span of a singleton in Hilbert space is a closed subspace. $)
    spansnchi $p |- ( span ` { A } ) e. CH $=
      ( chil wcel csn cspn cfv cch spansnch ax-mp ) ACDAEFGHDBAIJ $.
      $( [3-Jun-2004] $)
  $}

  $( A vector belongs to the span of its singleton. $)
  spansnid $p |- ( A e. ~H -> A e. ( span ` { A } ) ) $=
    ( chil wcel csn cort cfv cspn h1did spansn eleqtrrd ) ABCAADZEFEFKGFAHAIJ
    $.
    $( [3-Jun-2004] $)

  $( A scalar product with a vector belongs to the span of its singleton. $)
  spansnmul $p |- ( ( A e. ~H /\ B e. CC ) ->
                  ( B .h A ) e. ( span ` { A } ) ) $=
    ( cc wcel chil csm co csn cspn cfv csh wa shmulclOLD imp an1s spansnsh
    spansnid jca sylan2 ancoms ) BCDZAEDZBAFGAHIJZDZUAUCKDZAUCDZLUDUBUEUAUFUDUE
    UAUFLUDBAUCMNOUBUEUFAPAQRST $.
    $( [3-Jun-2004] $)

  $( A member of a span of a singleton is a vector. $)
  elspansncl $p |- ( ( A e. ~H /\ B e. ( span ` { A } ) ) -> B e. ~H ) $=
    ( csn chil wss cspn cfv wcel elspancl snssi sylan ) ACZDEBLFGHBDHADHLBIADJK
    $.
    $( [17-Dec-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Membership in the span of a singleton. $)
    elspansn $p |- ( A e. ~H ->
                   ( B e. ( span ` { A } ) <-> E. x e. CC B = ( x .h A ) ) ) $=
      ( chil wcel csn cspn cfv cv csm co wceq cc wrex wb c0v cif sneq fveq2d
      eleq2d opreq2 eqeq2d rexbidv bibi12d ax-hv0cl elimel elspansni dedth ) BD
      EZCBFZGHZEZCAIZBJKZLZAMNZOCUIBPQZFZGHZEZCUMUQJKZLZAMNZOBPBUQLZULUTUPVCVDU
      KUSCVDUJURGBUQRSTVDUOVBAMVDUNVACBUQUMJUAUBUCUDAUQCBPDUEUFUGUH $.
      $( [5-Jun-2004] $)
  $}

  $( Membership in the span of a singleton.  All members are collinear with the
     generating vector. $)
  elspansn2 $p |- ( ( A e. ~H /\ B e. ~H /\ B =/= 0h ) ->
  ( A e. ( span ` { B } ) <-> A = ( ( ( A .ih B ) / ( B .ih B ) ) .h B ) ) ) $=
    ( chil wcel c0v wne w3a csn cspn cfv cort csp co cdiv csm wceq wb spansn
    eleq2d 3ad2ant2 wi cif eleq1 id opreq1 opreq1d eqeq12d bibi12d imbi2d
    neeq1 sneq fveq2d opreq2 eqtrd opreq12d eqeq2d imbi12d ax-hv0cl elimel
    h1de2bi dedth2h 3impia bitrd ) ACDZBCDZBEFZGABHZIJZDZAVGKJZKJZDZAABLMZBBLMZ
    NMZBOMZPZVEVDVIVLQVFVEVHVKABRSTVDVEVFVLVQQZVDVEVFVRUAVFVDAEUBZVKDZVSVSBLMZV
    NNMZBOMZPZQZUAVEBEUBZEFZVSWFHZKJZKJZDZVSVSWFLMZWFWFLMZNMZWFOMZPZQZUAABEEAVS
    PZVRWEVFWRVLVTVQWDAVSVKUCWRAVSVPWCWRUDWRVOWBBOWRVMWAVNNAVSBLUEUFUFUGUHUIBWF
    PZVFWGWEWQBWFEUJWSVTWKWDWPWSVKWJVSWSVJWIKWSVGWHKBWFUKULULSWSWCWOVSWSWBWNBWF
    OWSWAWLVNWMNBWFVSLUMWSVNWFBLMWMBWFBLUEBWFWFLUMUNUOWSUDUOUPUHUQVSWFAECURUSBE
    CURUSUTVAVBVC $.
    $( [5-Jun-2004] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x C $.
    $( The singletons of collinear vectors have the same span. $)
    spansncol $p |- ( ( A e. ~H /\ B e. CC /\ B =/= 0 ) ->
                    ( span ` { ( B .h A ) } ) = ( span ` { A } ) ) $=
      ( vx vy vz chil wcel cc cc0 wne w3a csm co csn cspn cfv cv wceq wrex wi
      wa cmul ax-hvmulass 3com13 3expa eqeq2d biimprd mulcl ancoms adantll
      jctild opreq1 rcla4ev syl6 r19.23adva 3adant3 cdiv divcl 3expb adantlr
      simprl simplr syl111anc divcan1 3exp imp32 opreq1d eqtr3d exp43 com4l
      3imp r19.23adv impbid wb hvmulcl elspansn syl 3ad2ant1 3bitr4d eqrdv ) AF
      GZBHGZBIJZKZCBALMZNOPZANOPZWDCQZDQZWELMZRZDHSZWHEQZALMZRZEHSZWHWFGZWHWGGZ
      WDWLWPWAWBWLWPTWCWAWBUAZWKWPDHWSWIHGZUAZWKWIBUBMZHGZWHXBALMZRZUAWPXAWKXEX
      CXAXEWKXAXDWJWHWAWBWTXDWJRZWTWBWAXFWIBAUCUDUEUFUGWBWTXCWAWTWBXCWIBUHUIUJU
      KWOXEEXBHWMXBRWNXDWHWMXBALULUFUMUNUOUPWDWOWLEHWAWBWCWMHGZWOWLTZTXGWAWBWCX
      HXGWAWBWCXHXGWAUAZWBWCUAZUAZWOWMBUQMZHGZWHXLWELMZRZUAWLXKWOXOXMXKXOWOXKXN
      WNWHXKXLBUBMZALMZXNWNXKXMWBWAXQXNRXGXJXMWAXGWBWCXMWMBURUSUTZXIWBWCVAXGWAX
      JVBXLBAUCVCXKXPWMALXGXJXPWMRZWAXGWBWCXSXGWBWCXSWMBVDVEVFUTVGVHUFUGXRUKWKX
      ODXLHWIXLRWJXNWHWIXLWELULUFUMUNVIVJVKVLVMWAWBWQWLVNZWCWSWEFGZXTWBWAYABAVO
      UIDWEWHVPVQUPWAWBWRWPVNWCEAWHVPVRVSVT $.
      $( [6-Jun-2004] $)
  $}

  $( Membership relation implied by equality of spans. $)
  spansneleqi $p |- ( A e. ~H ->
        ( ( span ` { A } ) = ( span ` { B } ) -> A e. ( span ` { B } ) ) ) $=
    ( csn cspn cfv wceq wcel chil eleq2 spansnid syl5cbi ) ACDEZBCDEZFALGAMGAHG
    LMAIAJK $.
    $( [6-Jun-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Membership relation that implies equality of spans. $)
    spansneleq $p |- ( ( B e. ~H /\ A =/= 0h ) ->
         ( A e. ( span ` { B } ) -> ( span ` { A } ) = ( span ` { B } ) ) ) $=
      ( vx chil wcel c0v wne wa csn cspn cfv cv csm co wceq cc wrex wb
      elspansn adantr sneq fveq2d ad2antll wi cc0 opreq1 ax-hvmul0 sylan9eqr
      ex eqeq1 biimprd sylan9 necon3d com23 imp3a spansncol 3expia syld exp4b
      imp43 eqtrd exp32 r19.23adv sylbid ) BDEZAFGZHZABIJKZEZACLZBMNZOZCPQZAIZJ
      KZVHOZVEVIVMRVFCBASTVGVLVPCPVGVJPEZVLVPVGVQVLHHVOVKIZJKZVHVLVOVSOVGVQVLVN
      VRJAVKUAUBUCVEVFVQVLVSVHOZVEVQVFVLVTUDVEVQVFVLVTVEVQHVFVLHZVJUEGZVTVEWAWB
      UDVQVEVFVLWBVEVLVFWBVEVLVFWBUDVEVLHVJUEAFVEVJUEOZVKFOZVLAFOZVEWCWDWCVEVKU
      EBMNFVJUEBMUFBUGUHUIVLWEWDAVKFUJUKULUMUIUNUOTVEVQWBVTBVJUPUQURUSUNUTVAVBV
      CVD $.
      $( [24-Jun-2006] $) $( [6-Jun-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The span of the singleton of an element of a subspace is included in the
       subspace. $)
    spansnss $p |- ( ( A e. SH /\ B e. A ) -> ( span ` { B } ) C_ A ) $=
      ( vx vy csh wcel wa csn cspn cfv cv csm co wceq cc wrex chil wb shel
      elspansn syl wi shmulclOLD eleq1 biimprd syl9 com23 exp3a imp r19.23adv
      sylbid ssrdv ) AEFZBAFZGZCBHIJZAUOCKZUPFZUQDKZBLMZNZDOPZUQAFZUOBQFURVBRBA
      SDBUQTUAUOVAVCDOUMUNUSOFZVAVCUBZUBUMVDUNVEUMVDUNVEUMVAVDUNGZVCUMVFUTAFZVA
      VCUSBAUCVAVCVGUQUTAUDUEUFUGUHUGUIUJUKUL $.
      $( [5-Jun-2004] $)
  $}

  $( A member of the span of the singleton of a vector is a member of a
     subspace containing the vector. $)
  elspansn3 $p |- ( ( A e. SH /\ B e. A /\ C e. ( span ` { B } ) ) ->
                   C e. A ) $=
    ( csh wcel csn cspn cfv wa spansnss sseld 3impia ) ADEZBAEZCBFGHZECAEMNIOAC
    ABJKL $.
    $( [19-Jun-2006] $) $( [16-Dec-2004] $)

  $( A span membership condition implying two vectors belong to the same
     subspace. $)
  elspansn4 $p |- ( ( ( A e. SH /\ B e. ~H ) /\
         ( C e. ( span ` { B } ) /\ C =/= 0h ) ) -> ( B e. A <-> C e. A ) ) $=
    ( csh wcel chil wa csn cspn cfv c0v wne wi elspansn3 3exp com23 imp
    ad2ant2r 3expia spansnid ad2antrr wceq spansneleq eleqtrrd syl5 exp4b
    com24 exp4a imp43 impbid ) ADEZBFEZGCBHIJZEZCKLZGGBAEZCAEZUKUNUPUQMZULUOUKU
    NURUKUPUNUQUKUPUNUQABCNOPQRUKULUNUOUQUPMZUKUNULUOUSMUKUNULUOUSUKUQULUOGZUNU
    PUKUQUTUNUPUKUQGBCHIJZEZUPUTUNGZUKUQVBUPACBNSVCBUMVAULBUMEUOUNBTUAUTUNVAUMU
    BCBUCQUDUEUFUGUHPUIUJ $.
    $( [27-Dec-2007] $) $( [17-Dec-2004] $)

  $( A vector belonging to both a subspace and the span of the singleton of a
     vector not in it must be zero. $)
  elspansn5 $p |- ( A e. SH -> ( ( ( B e. ~H /\ -. B e. A ) /\
         ( C e. ( span ` { B } ) /\ C e. A ) ) -> C = 0h ) ) $=
    ( csh wcel chil wn csn cspn cfv wa c0v wceq wi wne elspansn4 biimprd exp32
    com34 imp32 necon1bd exp31 imp4c ) ADEZBFEZBAEZGZCBHIJEZCAEZKZCLMZUDUEUJUGU
    KUDUEUJUGUKNUDUEKZUJKUFCLULUHUICLOZUFNULUHUMUIUFULUHUMUIUFNULUHUMKKUFUIABCP
    QRSTUAUBSUC $.
    $( [17-Dec-2004] $)

  $( The span of the singleton of an element of a subspace is included in the
     subspace. $)
  spansnss2 $p |- ( ( A e. SH /\ B e. ~H ) ->
                   ( B e. A <-> ( span ` { B } ) C_ A ) ) $=
    ( csh wcel chil wa csn cspn cfv wss wi spansnss ex adantr ssel spansnid
    syl5com adantl impbid ) ACDZBEDZFBADZBGHIZAJZTUBUDKUATUBUDABLMNUAUDUBKTUDBU
    CDUBUAUCABOBPQRS $.
    $( [16-Dec-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Cancellation-type law that "extracts" a vector ` A ` from its inner
       product with a proportional vector ` B ` . $)
    normcan $p |- ( ( B e. ~H /\ B =/= 0h /\ A e. ( span ` { B } ) ) ->
            ( ( ( A .ih B ) / ( ( normh ` B ) ^ 2 ) ) .h B ) = A ) $=
      ( vx chil wcel c0v wne csn cspn cfv csp co cno c2 cexp cdiv csm wceq wa
      cv cc wrex wb elspansn adantr cmul opreq1 simpr simpl ax-his3 syl111anc
      sylan9eqr normsq ad2antrr opreq12d adantllr cc0 hicl anidms his6
      necon3bid biimpar divcan4 eqtrd opreq1d eqtr4d exp31 r19.23adv sylbid
      3impia ) BDEZBFGZABHIJEZABKLZBMJNOLZPLZBQLZARZVKVLSZVMACTZBQLZRZCUAUBZVRV
      KVMWCUCVLCBAUDUEVSWBVRCUAVSVTUAEZWBVRVSWDSZWBSZVQWAAWFVPVTBQWFVPVTBBKLZUF
      LZWGPLZVTVKWDWBVPWIRVLVKWDSZWBSVNWHVOWGPWBWJVNWABKLZWHAWABKUGWJWDVKVKWKWH
      RVKWDUHVKWDUIZWLVTBBUJUKULVKVOWGRWDWBBUMUNUOUPWEWIVTRZWBWEWDWGUAEZWGUQGZW
      MVSWDUHVKWNVLWDVKWNBBURUSUNVSWOWDVKWOVLVKWGUQBFBUTVAVBUEVTWGVCUKUEVDVEWEW
      BUHVFVGVHVIVJ $.
      $( [18-Mar-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A projection on the span of a singleton. $)
    pjspansn $p |- ( ( A e. ~H /\ B e. ~H /\ A =/= 0h ) -> ( ( proj ` ( span `
        { A } ) ) ` B ) = ( ( ( B .ih A ) / ( ( normh ` A ) ^ 2 ) ) .h A ) ) $=
      ( vx vy chil wcel c0v wne w3a csn cspn cfv cpj cv cva co wceq cort wrex
      crab cuni csp cno c2 cexp cdiv csm cch pjval spansnch sylan 3adant3 wss
      wi wa opreq1 ad2antll caddc cc0 chel choccl syl adantlr ax-his2 3comr
      3expa syldan csh spansnsh adantr spansnid simpr shocorth 3impib
      syl111anc wb orthcom mpbid opreq2d cc hicl ancoms addid1 3eqtrd simpl
      elspansncl jca adantrr eqtrd adantllr opreq1d normcan eqtr2d exp32
      r19.23adv 3adantl2 ss2rabdv wreu pjtheu reuunisn simp1 normcl recnd sqcl
      3ad2ant1 sqne0 normne0 bitr2d biimpa 3adant2 divcl spansnmul syl11anc
      rabsn 3sstr3d fvex rabex uniex snsssn ) AEFZBEFZAGHZIZBAJZKLZMLLZBCNZDNZO
      PZQZDYERLZSZCYETZUAZBAUBPZAUCLZUDUEPZUFPZAUGPZXTYAYFYNQZYBYEUHFZYAYTXTCDB
      YEUIAUJZUKULYCYNJZYSJZUMYNYSQYCYMYGYSQZCYETZUUCUUDYCYLUUECYEXTYBYGYEFZYLU
      UEUNYAXTYBUOUUGUOZYJUUEDYKUUHYHYKFZYJUUEUUHUUIYJUOZUOZYSYGAUBPZYQUFPZAUGP
      ZYGUUKYRUUMAUGUUKYOUULYQUFXTUUGUUJYOUULQYBXTUUGUOZUUJUOYOYIAUBPZUULYJYOUU
      PQUUOUUIBYIAUBUPUQUUOUUIUUPUULQZYJXTYGEFZUOZUUIUUQUUOUUSUUIUOZUUPUULYHAUB
      PZURPZUULUSURPZUULUUSUUIYHEFZUUPUVBQZXTUUIUVDUURYKUHFZUUIUVDXTYHYKUTXTUUA
      UVFUUBYEVAVBUKZVCXTUURUVDUVEUURUVDXTUVEYGYHAVDVEVFVGUUTUVAUSUULURXTUUIUVA
      USQZUURXTUUIUOZAYHUBPUSQZUVHUVIYEVHFZAYEFZUUIUVJXTUVKUUIAVIVJXTUVLUUIAVKV
      JXTUUIVLUVKUVLUUIUVJAYHYEVMVNVOXTUUIUVDUVJUVHVPUVGAYHVQVGVRVCVSUUSUVCUULQ
      ZUUIUUSUULVTFZUVMUURXTUVNYGAWAWBUULWCVBVJWDUUOXTUURXTUUGWEAYGWFWGUKWHWIWJ
      WKWKUUHUUNYGQZUUJXTYBUUGUVOYGAWLVFVJWMWNWOWPWQYCYLCYEWRZYMUUCQXTYAUVPYBUU
      AYAUVPXTCDBYEWSUUBUKULYLCYEWTVBYCYSYEFZUUFUUDQYCXTYRVTFZUVQXTYAYBXAYCYOVT
      FZYQVTFZYQUSHZUVRXTYAUVSYBYAXTUVSBAWAWBULXTYAUVTYBXTYPVTFZUVTXTYPAXBXCZYP
      XDVBXEXTYBUWAYAXTYBUWAXTUWAYPUSHZYBXTUWBUWAUWDVPUWCYPXFVBAXGXHXIXJYOYQXKV
      OAYRXLXMCYEYSXNVBXOYNYSYMYLCYEYDKXPXQXRXSVBWI $.
      $( [28-May-2006] $)
  $}

  ${
    spansnpj.1 $e |- A C_ ~H $.
    spansnpj.2 $e |- B e. ~H $.
    $( A subset of Hilbert space is orthogonal to the span of the singleton of
       a projection onto its orthocomplement. $)
    spansnpji $p |- A C_
                 ( _|_ ` ( span ` { ( ( proj ` ( _|_ ` A ) ) ` B ) } ) ) $=
      ( cort cfv cpj csn cspn chil wss ococss ax-mp occli chssii wcel pjclii
      snssi spanss mp2an csh wceq chshii spanid sseqtri pjhclii spansnchi
      chsscon3i mpbi sstri ) AAEFZEFZBUKGFFZHZIFZEFZAJKAULKCALMUOUKKULUPKUOUKIF
      ZUKUKJKUNUKKZUOUQKUKACNZOUMUKPURBUKUSDQUMUKRMUNUKSTUKUAPUQUKUBUKUSUCUKUDM
      UEUOUKUMBUKUSDUFUGUSUHUIUJ $.
      $( [4-Jun-2004] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.
    spanunsn.1 $e |- A e. CH $.
    spanunsn.2 $e |- B e. ~H $.
    $( The span of the union of a closed subspace with a singleton equals the
       span of its union with an orthogonal singleton. $)
    spanunsni $p |- ( span ` ( A u. { B } ) ) =
             ( span ` ( A u. { ( ( proj ` ( _|_ ` A ) ) ` B ) } ) ) $=
      ( vx vy vz vw vv vu csn cspn cfv cph co cort cpj cun cv wcel cva wceq
      wrex chshii chil wss csh snssi ax-mp spancl shseli csm cc wi wa eleq1
      biimparc shaddclOLD pjclii shmulclOLD mpan2 sylan2i choccli pjhclii
      spansnmul mpan adantl ax-hvdistr1 mp3an23 pjpji opreq2i syl5eq opreq2d
      ax-hvass 3expb cheli hvmulcl jca syl2an eqtr4d rcla4eopr syl111anc
      sylibr opreq2 eqeq2d biimpa exp43 r19.23adv elspansni syl5ib r19.23aiv
      sylbi cneg negcl syl sylani ancoms c0v c1 hvm1neg hvnegid ax-hvcom
      syl11anc 3eqtr3d opreq1d hvaddcl hvaddid2 anim12i hvadd4 impbii eqriv
      chssii spanuni spanid opreq1i eqtri 3eqtr4i ) ABKZLMZNOZABAPMZQMMZKZLMZNO
      ZAXRRLMZAYCRLMZEXTYEESZXTTZYHYETZYIYHFSZGSZUAOZUBZGXSUCZFAUCYJFGAXSYHACUD
      ZXRUEUFZXSUGTBUETZYQDBUEUHUIZXRUJUIZUKYOYJFAYKATZYNYJGXSUUAYLHSZBULOZUBZH
      UMUCYNYJUNZYLXSTUUAUUDUUEHUMUUAUUBUMTZUUDYNYJYKUUCUAOZYETZYHUUGUBZYJUUAUU
      FUOZUUDYNUOUUIYJUUHYHUUGYEUPUQUUJUUGISJSUAOZUBJYDUCIAUCZUUHUUJYKUUBBAQMMZ
      ULOZUAOZATZUUBYBULOZYDTZUUGUUOUUQUAOZUBUULAUGTZUUJUUPUNYPUUTUUAUUNATZUUPU
      UFYKUUNAURUUFUUMATZUVABACDUSZUUTUUFUVBUOUVAUNYPUUBUUMAUTUIVAVBUIUUFUURUUA
      YBUETZUUFUURBYAACVCDVDZYBUUBVEVFVGUUJUUGYKUUNUUQUAOZUAOZUUSUUJUUCUVFYKUAU
      UFUUCUVFUBUUAUUFUUBUUMYBUAOZULOZUVFUUCUUFUUMUETZUVDUVIUVFUBBACDVDZUVEUUBU
      UMYBVHVIBUVHUUBULBACDVJVKVLVGZVMYKUETZUUNUETZUUQUETZUOUUSUVGUBZUUAUUFUVMU
      VNUVOUVPYKUUNUUQVNVOYKACVPZUUFUVNUVOUUFUVJUVNUVKUUBUUMVQVAZUUFUVDUVOUVEUU
      BYBVQVAZVRVSVTIJAYDUUOUUQUUGUAWAWBIJAYDUUGYPYCUEUFZYDUGTUVDUVTUVEYBUEUHUI
      ZYCUJUIZUKWCUUDYNUUIUUDYMUUGYHYLUUCYKUAWDWEWFVSWGWHHBYLDWIWJWHWKWLYJYNGYD
      UCZFAUCYIFGAYDYHYPUWBUKUWCYIFAUUAYNYIGYDUUAYLUUQUBZHUMUCYNYIUNZYLYDTUUAUW
      DUWEHUMUUAUUFUWDYNYIYKUUQUAOZXTTZYHUWFUBZYIUUJUWDYNUOUWHYIUWGYHUWFXTUPUQU
      UJUWFUUKUBJXSUCIAUCZUWGUUJUUBWMZUUMULOZYKUAOZATZUUCXSTZUWFUWLUUCUAOZUBUWI
      UUFUUAUWMUUTUUFUUAUOUWMUNYPUUTUWKATZUUAUWMUUFUWKYKAURUUFUWJUMTZUWPUUBWNZU
      WQUVBUWPUVCUUTUWQUVBUOUWPUNYPUWJUUMAUTUIVAWOWPUIWQUUFUWNUUAYRUUFUWNDBUUBV
      EVFVGUUJUWFUWLUVFUAOZUWOUUJWRUWFUAOZUWKUUNUAOZUWFUAOZUWFUWSUUJWRUXAUWFUAU
      UFWRUXAUBUUAUUFUUNWSWMUUNULOZUAOZUUNUWKUAOZWRUXAUUFUXCUWKUUNUAUUFUVJUXCUW
      KUBUVKUUBUUMWTVAVMUUFUVNUXDWRUBUVRUUNXAWOUUFUVNUWKUETZUXEUXAUBUVRUUFUWQUX
      FUWRUWQUVJUXFUVKUWJUUMVQVAWOZUUNUWKXBXCXDVGXEUUJUWFUETZUWTUWFUBUVMUVOUXHU
      UAUUFYKUUQXFUVQUVSVSUWFXGWOUUJUXFUVNUOZUVMUVOUOUXBUWSUBUUFUXIUUAUUFUXFUVN
      UXGUVRVRVGUUAUVMUUFUVOUVQUVSXHUWKUUNYKUUQXIXCXDUUJUUCUVFUWLUAUVLVMVTIJAXS
      UWLUUCUWFUAWAWBIJAXSUWFYPYTUKWCUWDYNUWHUWDYMUWFYHYLUUQYKUAWDWEWFVSWGWHHYB
      YLUVEWIWJWHWKWLXJXKYFALMZXSNOXTAXRACXLZYSXMUXJAXSNUUTUXJAUBYPAXNUIZXOXPYG
      UXJYDNOYEAYCUXKUWAXMUXJAYDNUXLXOXPXQ $.
      $( [3-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The span of a pair of vectors. $)
    spanpr $p |- ( ( A e. ~H /\ B e. ~H ) ->
                 ( span ` { ( A +h B ) } ) C_ ( span ` { A , B } ) ) $=
      ( vx chil wcel wa cva co csn cspn cfv cph cpr cv csh shscl spansnsh
      syl2an adantr anim12i spansnid shsva sylc simpr elspansn3 syl111anc ex
      ssrdv cun wss wceq spanun snssi df-pr fveq2i syl5req sseqtrd ) ADEZBDEZFZ
      ABGHZIJKZAIZJKZBIZJKZLHZABMZJKZUTCVBVGUTCNZVBEZVJVGEZUTVKFVGOEZVAVGEZVKVL
      UTVMVKVDOEZVFOEZVMURUSVDVFPAQZBQZRSUTVNVKUTVOVPFAVDEZBVFEZFVNURVOUSVPVQVR
      TURVSUSVTAUABUATVDVFABUBUCSUTVKUDVGVAVJUEUFUGUHUTVCVEUIZJKZVGVIVCDUJVEDUJ
      WBVGUKURUSVCVEULADUMBDUMRVHWAJABUNUOUPUQ $.
      $( [9-Jun-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    h1datom.1 $e |- A e. CH $.
    h1datom.2 $e |- B e. ~H $.
    $( A 1-dimensional subspace is an atom. $)
    h1datomi $p |- ( A C_ ( _|_ ` ( _|_ ` { B } ) ) ->
                  ( A = ( _|_ ` ( _|_ ` { B } ) ) \/ A = 0H ) ) $=
      ( vx vy csn cort cfv wss wne c0h wceq wi wo wa wcel cv c0v wrex ssel csm
      co cc cc0 eqeq1 opreq1 chil ax-hvmul0 ax-mp syl6eq syl5bir necon3d
      adantl c1 cdiv reccl csh chshii shmulcl mp3an1 ex syl adantr opreq2 cmul
      simpl ax-hvmulass mp3an3 syl11anc recid2 opreq1d eqtr3d ax-hvmulid
      sylan9eqr eleq1d sylibd exp31 com23 imp syld com3r exp3a r19.23adv
      h1de2ci syl5ib sylcom chne0i snssi chssii occon2i ococi syl6ss syl6
      anc2li eqss syl6ibr necon1d neor sylibr ) ABGZHIHIZJZAXBKALMZNAXBMZXDOXCA
      LAXBXCALKZXCXBAJZPXEXCXFXGXCXFBAQZXGXCERZSKZEATXHXFXCXJXHEAXCXIAQZXIXBQZX
      JXHNZAXBXIUAXKXIFRZBUBUCZMZFUDTXMXLXKXPXMFUDXKXNUDQZXPXMXQXPPZXJXKXHXRXJX
      NUEKZXKXHNZXPXJXSNXQXPXNUEXISXPXISMXOSMXNUEMZXIXOSUFYAXOUEBUBUCZSXNUEBUBU
      GBUHQZYBSMDBUIUJUKULUMUNXQXPXSXTNXQXSXPXTXQXSXPXTXQXSPZXPPZXKUOXNUPUCZXIU
      BUCZAQZXHYDXKYHNZXPYDYFUDQZYIXNUQZYJXKYHAURQYJXKYHACUSYFXIAUTVAVBVCVDYEYG
      BAXPYDYGYFXOUBUCZBXIXOYFUBVEYDYLUOBUBUCZBYDYFXNVFUCZBUBUCZYLYMYDYJXQYOYLM
      ZYKXQXSVGYJXQYCYPDYFXNBVHVIVJYDYNUOBUBXNVKVLVMYCYMBMDBVNUJUKVOVPVQVRVSVTW
      AWBWCWDFXIBDWEWFWGWDEACWHWFXHXBAHIHIZAXHXAAJXBYQJBAWIXAAYCXAUHJDBUHWIUJAC
      WJWKVCACWLWMWNWOAXBWPWQWRXDAXBWSWT $.
      $( [20-Jul-2001] $)
  $}

  $( A 1-dimensional subspace is an atom. $)
  h1datom $p |- ( ( A e. CH /\ B e. ~H ) ->
                ( A C_ ( _|_ ` ( _|_ ` { B } ) ) ->
                ( A = ( _|_ ` ( _|_ ` { B } ) ) \/ A = 0H ) ) ) $=
    ( cch wcel chil csn cort cfv wss wceq c0h wo wi cif c0v sseq1 eqeq1
    orbi12d imbi12d sneq fveq2d sseq2d eqeq2d orbi1d h0elch elimel ax-hv0cl
    h1datomi dedth2h ) ACDZBEDZABFZGHZGHZIZAUNJZAKJZLZMUJAKNZUNIZUSUNJZUSKJZLZM
    USUKBONZFZGHZGHZIZUSVGJZVBLZMABKOAUSJZUOUTURVCAUSUNPVKUPVAUQVBAUSUNQAUSKQRS
    BVDJZUTVHVCVJVLUNVGUSVLUMVFGVLULVEGBVDTUAUAZUBVLVAVIVBVLUNVGUSVMUCUDSUSVDAK
    CUEUFBOEUGUFUHUI $.
    $( [22-Jul-2001] $)

