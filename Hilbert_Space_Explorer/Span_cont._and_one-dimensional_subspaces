$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Span (cont.) and one-dimensional subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The span of the singleton of the zero vector is the zero subspace. $)
  spansn0 $p |- ( span ` { 0h } ) = 0H $=
    ( c0h cspn cfv c0v csn df-ch0 fveq2i wcel wceq h0elsh spanid ax-mp eqtr3i
    csh ) ABCZDEZBCAAPBFGANHOAIJAKLM $.
    $( [14-Jan-2005] $)

  $( The span of the empty set is the zero subspace.  Remark 11.6.e of
     [Schechter] p. 276. $)
  span0 $p |- ( span ` (/) ) = 0H $=
    ( cspn cfv c0h chil wss h0elsh shssii 0ss spanss mp2an csh wcel wceq spanid
    c0 ax-mp sseqtri spancl sh0le eqssi ) OABZCUACABZCCDEOCEUAUBECFGCHOCIJCKLUB
    CMFCNPQUAKLZCUAEODEUCDHORPUASPT $.
    $( [3-Jun-2004] $)

  ${
    $d x A $.  $d x B $.
    elspan.1 $e |- B e. _V $.
    $( Membership in the span of a subset of Hilbert space. $)
    elspani $p |- ( A C_ ~H ->
                ( B e. ( span ` A ) <-> A. x e. SH ( A C_ x -> B e. x ) ) ) $=
      ( chil wss cspn cfv wcel cv csh crab cint wi wral spanval eleq2d elintrab
      syl6bb ) BEFZCBGHZICBAJZFZAKLMZIUCCUBINAKOTUAUDCABPQUCACKDRS $.
      $( [2-Jun-2004] $)

  $}

  ${
    spanun.1 $e |- A C_ ~H $.
    spanun.2 $e |- B C_ ~H $.
    $d x y z w A $.  $d x y z w B $.
    $( The span of a union is the subspace sum of spans. $)
    spanuni $p |- ( span ` ( A u. B ) ) = ( ( span ` A ) +H ( span ` B ) ) $=
      ( vx vz vw vy cspn cfv chil wss csh wcel ax-mp cv wa wel wi wral co mp2an
      cun spancl shscli shssii spanss2 unss12 shunssi sstri spanss wceq sseqtri
      cph spanid cva wex wrex shseli bitri wb vex elspani anbi12i r19.26 bitr4i
      r2ex sylanb unss prth syl5bir shaddcl 3expib sylan9r eleq1 biimprd sylan9
      r19.27av expl ralimia unssi sylibr syl exlimivv sylbi ssriv eqssi ) ABUCZ
      IJZAIJZBIJZUNUAZWIWLIJZWLWLKLWHWLLWIWMLWLWJWKAKLZWJMNCAUDOZBKLZWKMNDBUDOZ
      UEZUFWHWJWKUCZWLAWJLZBWKLZWHWSLWNWTCAUGOWPXADBUGOAWJBWKUHUBWJWKWOWQUIUJWH
      WLUKUBWLMNWMWLULWRWLUOOUMEWLWIEPZWLNZFPZWJNZGPZWKNZQZXBXDXFUPUAZULZQZGUQF
      UQZXBWINZXCXJGWKURFWJURXLFGWJWKXBWOWQUSXJFGWJWKVGUTXKXMFGXKAHPZLZFHRZSZBX
      NLZGHRZSZQZXJQZHMTZXMXHYAHMTZXJYCXHXQHMTZXTHMTZQYDXEYEXGYFWNXEYEVACHAXDFV
      BVCOWPXGYFVADHBXFGVBVCOVDXQXTHMVEVFYAXJHMVRVHYCWHXNLZEHRZSZHMTZXMYBYIHMXN
      MNZYAXJYIYKYAQYGXIXNNZXJYHYAYGXPXSQZYKYLYGXOXRQYAYMABXNVIXOXPXRXSVJVKYKXP
      XSYLXDXFXNVLVMVNXJYHYLXBXIXNVOVPVQVSVTWHKLXMYJVAABKCDWAHWHXBEVBVCOWBWCWDW
      EWFWG $.
      $( [2-Jun-2004] $)
  $}

  $( The span of a union is the subspace sum of spans. $)
  spanun $p |- ( ( A C_ ~H /\ B C_ ~H ) ->
                ( span ` ( A u. B ) ) = ( ( span ` A ) +H ( span ` B ) ) ) $=
    ( chil wss cun cspn cfv cph co cif uneq1 fveq2d fveq2 opreq1d eqeq12d uneq2
    wceq opreq2d sseq1 elimhyp ssid spanuni dedth2h ) ACDZBCDZABEZFGZAFGZBFGZHI
    ZQUDACJZBEZFGZUKFGZUIHIZQUKUEBCJZEZFGZUNUPFGZHIZQABCCAUKQZUGUMUJUOVAUFULFAU
    KBKLVAUHUNUIHAUKFMNOBUPQZUMURUOUTVBULUQFBUPUKPLVBUIUSUNHBUPFMROUKUPUDUKCDCC
    DZACAUKCSCUKCSCUAZTUEUPCDVCBCBUPCSCUPCSVDTUBUC $.
    $( [9-Jun-2006] $)

  ${
    sshjococ.1 $e |- A C_ ~H $.
    sshjococ.2 $e |- B C_ ~H $.
    $( The join of two Hilbert space subsets (not necessarily closed subspaces)
       equals the join of their closures (double orthocomplements). $)
    sshhococi $p |- ( A vH B ) =
                 ( ( _|_ ` ( _|_ ` A ) ) vH ( _|_ ` ( _|_ ` B ) ) ) $=
      ( chj cort cfv cun wss chil ococss ax-mp mp2an unssi occli choccli chssii
      co occon2i sstri unss12 sshjval chjvali 3sstr4i ssun1 sseqtr4i cch sshjcl
      wceq wcel ssun2 chlubii ococi sseqtri eqssi ) ABERZAFGZFGZBFGZFGZERZABHZF
      GFGZURUTHZFGFGZUPVAVBVDIZVCVEIAURIZBUTIZVFAJIZVGCAKLBJIZVHDBKLAURBUTUAMVB
      VDABJCDNZURUTJURUQACOPZQUTUSBDOPZQNSLVIVJUPVCUICDABUBMZURUTVLVMUCUDVAUPFG
      ZFGZUPURVPIZUTVPIZVAVPIAUPIVQAVCUPAVBVCABUEVBJIVBVCIVKVBKLZTVNUFAUPCUPVIV
      JUPUGUJCDABUHMZQZSLBUPIVRBVCUPBVBVCBAUKVSTVNUFBUPDWASLURUTVPVLVMVOUPVTPPU
      LMUPVTUMUNUO $.
      $( [1-Jun-2004] $)
  $}

  $( Hilbert space has a nonzero vector iff it is not trivial. $)
  hne0 $p |- ( ~H =/= 0H <-> E. x e. ~H x =/= 0h ) $=
    ( chil helch chne0i ) ABCD $.
    $( [24-Feb-2006] $)

  $( The supremum of the empty set. $)
  chsup0 $p |- ( \/H ` (/) ) = 0H $=
    ( c0 chsup cfv c0h wss wceq csn 0ss cch wi h0elch snssi ax-mp chsupss mp2an
    wcel chsupsn sseqtri chsupcl chle0i mpbi ) ABCZDEUBDFUBDGZBCZDAUCEZUBUDEZUC
    HAIEZUCIEZUEUFJIHZDIPZUHKDILMAUCNOMUJUDDFKDQMRUBUGUBIPUIASMTUA $.
    $( [13-Aug-2002] $)

  ${
    $d x A $.  $d x B $.
    h1deot.1 $e |- B e. ~H $.
    $( Membership in orthocomplement of 1-dimensional subspace. $)
    h1deoi $p |- ( A e. ( _|_ ` { B } ) <->
                 ( A e. ~H /\ ( A .ih B ) = 0 ) ) $=
      ( vx csn cort cfv wcel chil cv csp co cc0 wceq wral wa wss ax-mp wi wal
      wb snssi ocel df-ral elsn imbi1i albii elexi opreq2 eqeq1d ceqsalv 3bitri
      anbi2i bitri ) ABEZFGHZAIHZADJZKLZMNZDUOOZPZUQABKLZMNZPUOIQZUPVBUABIHVECB
      IUBRDAUOUCRVAVDUQVAURUOHZUTSZDTURBNZUTSZDTVDUTDUOUDVGVIDVFVHUTDBUEUFUGUTV
      DDBBICUHVHUSVCMURBAKUIUJUKULUMUN $.
      $( [7-Jul-2001] $)

    $( Membership in 1-dimensional subspace. $)
    h1dei $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
    ( A e. ~H /\ A. x e. ~H ( ( B .ih x ) = 0 -> ( A .ih x ) = 0 ) ) ) $=
      ( csn cort cfv wcel chil csp co cc0 wceq wral wa wi wss wb ax-mp bitri cv
      snssi occli chssii ocel h1deoi orthcom mpan2 pm5.32i imbi1i impexp anbi2i
      ralbii2 ) BCEZFGZFGHZBIHZBAUAZJKLMZAUONZOZUQCURJKLMZUSPZAINZOUOIQUPVARUOU
      NCIHZUNIQDCIUBSUCUDABUOUESUTVDUQUSVCAUOIURUOHZUSPURIHZVBOZUSPVGVCPVFVHUSV
      FVGURCJKLMZOVHURCDUFVGVIVBVGVEVIVBRDURCUGUHUITUJVGVBUSUKTUMULT $.
      $( [7-Jul-2001] $)
  $}

  $( A generating vector belongs to the 1-dimensional subspace it generates. $)
  h1did $p |- ( A e. ~H -> A e. ( _|_ ` ( _|_ ` { A } ) ) ) $=
    ( chil wcel csn cort cfv wss snssi ococss syl snssg mpbird ) ABCZAADZEFEFZC
    NOGZMNBGPABHNIJAOBKL $.
    $( [22-Jul-2001] $)

  $( A nonzero vector generates a (nonzero) 1-dimensional subspace. $)
  h1dn0 $p |- ( ( A e. ~H /\ A =/= 0h ) ->
              ( _|_ ` ( _|_ ` { A } ) ) =/= 0H ) $=
    ( chil wcel c0v wne csn cort cfv h1did eleq2 syl5ibcom elch0 syl6ib necon3d
    c0h wceq imp ) ABCZADEAFGHGHZOERSOADRSOPZAOCZADPRASCTUAAISOAJKALMNQ $.
    $( [19-Jun-2006] $) $( [22-Jul-2001] $)

  ${
    $d x A $.  $d x B $.
    h1de2.1 $e |- A e. ~H $.
    h1de2.2 $e |- B e. ~H $.
    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2i $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) ->
                ( ( B .ih B ) .h A ) = ( ( A .ih B ) .h B ) ) $=
      ( vx cort cfv wcel csp co cmul wceq csm cmin cc0 chil hicli mp3an ax-his3
      wi csn cmv hvmulcli his2sub mulcomi eqtri opreq12i eqtr2i 3eqtr4i subeq0i
      cc mpbir cv wral h1dei mpbiran hvsubcli opreq2 eqeq1d imbi12d ax-mp sylbi
      rcla4v wb orthcom mp2an 3imtr4g mpi syl5eq mulcli sylib eqcomd bcseqi ) A
      BUAFGFGHZABIJZBAIJZKJZAAIJZBBIJZKJZLVSAMJZVOBMJZLVNVTVQVNVTVQNJZOLVTVQLVN
      WCWAWBUBJZAIJZOWEWAAIJZWBAIJZNJZWCWAPHZWBPHZAPHZWEWHLVSABBDDQZCUCZVOBABCD
      QZDUCZCWAWBAUDRWFVTWGVQNWFVSVRKJZVTVSUKHZWKWKWFWPLWLCCVSAASRVSVRWLAACCQZU
      EUFVOUKHZBPHZWKWGVQLWNDCVOBASRUGUHVNWDBIJZOLZWEOLZXAWABIJZWBBIJZNJZOWIWJW
      TXAXFLWMWODWAWBBUDRXFOLXDXELVSVOKJZVOVSKJZXDXEVSVOWLWNUEWQWKWTXDXGLWLCDVS
      ABSRWSWTWTXEXHLWNDDVOBBSRUIXDXEWABWMDQWBBWODQUJULUFVNBWDIJZOLZAWDIJZOLZXB
      XCVNBEUMZIJZOLZAXMIJZOLZTZEPUNZXJXLTZVNWKXSEABDUOCUPWDPHZXSXTTWAWBWMWOUQZ
      XRXTEWDPXMWDLZXOXJXQXLYCXNXIOXMWDBIURUSYCXPXKOXMWDAIURUSUTVCVAVBYAWTXBXJV
      DYBDWDBVEVFYAWKXCXLVDYBCWDAVEVFVGVHVIVTVQVRVSWRWLVJVOVPWNBADCQVJUJVKVLABC
      DVMVK $.
      $( [17-Jul-2001] $)

    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2bi $p |- ( B =/= 0h -> ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                A = ( ( ( A .ih B ) / ( B .ih B ) ) .h B ) ) ) $=
      ( c0v wne csp co cc0 cort cfv wcel csm wceq chil ax-mp c1 cmul cc syl csn
      cdiv wb his6 necon3bii wa h1de2i adantl opreq2d hicli recclzi ax-hvmulass
      mp3an23 ax-1cn divcan1zi opreq1d eqtr3d ax-hvmulid syl6eq adantr divreczi
      mulcom sylancl eqtr4d 3eqtr3d ex divclzi csh wss elexi snss occli choccli
      mpbi chshii h1did shmulcl mp3an13 eleq1 syl5ibrcom impbid sylbir ) BEFBBG
      HZIFZABUAZJKZJKZLZAABGHZWCUBHZBMHZNZUCWCIBEBOLZWCINBENUCDBUDPUEWDWHWLWDWH
      WLWDWHUFZQWCUBHZWCAMHZMHZWOWIBMHZMHZAWKWNWPWRWOMWHWPWRNWDABCDUGUHUIWDWQAN
      WHWDWQQAMHZAWDWOWCRHZAMHZWQWTWDWOSLZXBWQNZWCBBDDUJZUKZXCWCSLAOLZXDXECWOWC
      AULUMTWDXAQAMQWCUNXEUOUPUQXGWTANCAURPUSUTWDWSWKNWHWDWOWIRHZBMHZWSWKWDXCXI
      WSNZXFXCWISLZWMXJABCDUJZDWOWIBULUMTWDXHWJBMWDXHWIWORHZWJWDXCXKXHXMNXFXLWO
      WIVBVCWIWCXLXEVAVDUPUQUTVEVFWDWHWLWKWGLZWDWJSLZXNWIWCXLXEVGWGVHLXOBWGLZXN
      WGWFWEWMWEOVIDBOBODVJVKVNVLVMVOWMXPDBVPPWJBWGVQVRTAWKWGVSVTWAWB $.
      $( [19-Jul-2001] $)

    $( Lemma for ~ h1de2ci . $)
    h1de2ctlem $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                 E. x e. CC A = ( x .h B ) ) $=
      ( csn cort cfv wcel csm co wceq c0v cc0 fveq2d chil ax-mp opreq1 eqeq2d
      cc cv wrex sneq eleq2d elexi elsnc hsn0elch ococi eleq2i ax-hvmul0 eqeq2i
      3bitr4ri syl6rbbr 0cn rcla4ev mpan syl6bir wne csp cdiv h1de2bi necon3bii
      wi wb his6 hicli divclzi sylbir sylan ex sylbid pm2.61ine csh snssi occli
      wss choccli chshii h1did shmulcl mp3an13 eleq1 syl5ibrcom rexlimiv impbii
      ) BCFZGHZGHZIZBAUAZCJKZLZATUBZWIWMVCCMCMLZWIBNCJKZLZWMWNWIBMFZGHZGHZIZWPW
      NWHWSBWNWGWRGWNWFWQGCMUCOOUDBWQIBMLWTWPBMBPDUEUFWSWQBWQUGUHUIWOMBCPIZWOML
      ECUJQUKULUMNTIWPWMUNWLWPANTWJNLWKWOBWJNCJRSUOUPUQCMURZWIBBCUSKZCCUSKZUTKZ
      CJKZLZWMBCDEVAXBXGWMXBXETIZXGWMXBXDNURXHXDNCMXAXDNLWNVDECVEQVBXCXDBCDEVFC
      CEEVFVGVHWLXGAXETWJXELWKXFBWJXECJRSUOVIVJVKVLWLWIATWJTIZWIWLWKWHIZWHVMIXI
      CWHIZXJWHWGWFXAWFPVPECPVNQVOVQVRXAXKECVSQWJCWHVTWABWKWHWBWCWDWE $.
      $( [10-Aug-2008] $) $( [19-Jul-2001] $)
  $}

  ${
    $d x A $.  $d x B $.
    h1de2ct.1 $e |- B e. ~H $.
    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2ci $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                 E. x e. CC A = ( x .h B ) ) $=
      ( csn cort cfv wcel chil cv csm co wceq cc wrex wss snssi eleq1 wb c0v
      ax-mp occli choccli cheli hvmulcl mpan2 syl5ibrcom rexlimiv eqeq1 rexbidv
      cif bibi12d ax-hv0cl elimel h1de2ctlem dedth pm5.21nii ) BCEZFGZFGZHZBIHZ
      BAJZCKLZMZANOZBUTUSURCIHZURIPDCIQUAUBUCUDVEVBANVCNHZVBVEVDIHZVHVGVIDVCCUE
      UFBVDIRUGUHVBVAVFSVBBTUKZUTHZVJVDMZANOZSBTBVJMZVAVKVFVMBVJUTRVNVEVLANBVJV
      DUIUJULAVJCBTIUMUNDUOUPUQ $.
      $( [21-Jul-2001] $)
  $}

  ${
    $d x y z A $.  $d x B $.
    spansn.1 $e |- A e. ~H $.
    $( The span of a singleton in Hilbert space equals its closure. $)
    spansni $p |- ( span ` { A } ) = ( _|_ ` ( _|_ ` { A } ) ) $=
      ( vx vy vz csn cspn cfv cort chil wss wcel snssi ax-mp spanssoc cv wi csh
      wa cc wral elexi snss csm wceq wrex h1de2ci shmulclOLD eleq1a com12 syl9r
      co exp4a com3r rexlimiv sylbi imp syl5bir ralrimiva wb vex elspani sylibr
      ssriv eqssi ) AFZGHZVFIHIHZVFJKZVGVHKAJLVIBAJMNZVFONCVHVGCPZVHLZVFDPZKZVK
      VMLZQZDRUAZVKVGLZVLVPDRVNAVMLZVLVMRLZSVOAVMAJBUBUCVLVTVSVOQZVLVKEPZAUDULZ
      UEZETUFVTWAQZEVKABUGWDWEETWDVTWBTLZWAWDVTWFVSVOVTWFVSSWCVMLZWDVOWBAVMUHWG
      WDVOWCVMVKUIUJUKUMUNUOUPUQURUSVIVRVQUTVJDVFVKCVAVBNVCVDVE $.
      $( [3-Jun-2004] $)

    $( Membership in the span of a singleton. $)
    elspansni $p |- ( B e. ( span ` { A } ) <-> E. x e. CC B = ( x .h A ) ) $=
      ( csn cspn cfv wcel cort cv csm co wceq wrex spansni eleq2i h1de2ci bitri
      cc ) CBEZFGZHCTIGIGZHCAJBKLMASNUAUBCBDOPACBDQR $.
      $( [3-Jun-2004] $)
  $}

  $( The span of a singleton in Hilbert space equals its closure. $)
  spansn $p |- ( A e. ~H -> ( span ` { A } ) = ( _|_ ` ( _|_ ` { A } ) ) ) $=
    ( chil wcel csn cspn cfv cort wceq c0v sneq fveq2d eqeq12d ax-hv0cl spansni
    cif elimel dedth ) ABCZADZEFZSGFZGFZHRAIOZDZEFZUDGFZGFZHAIAUCHZTUEUBUGUHSUD
    EAUCJZKUHUAUFGUHSUDGUIKKLUCAIBMPNQ $.
    $( [4-Jun-2004] $)

  $( The span of a Hilbert space singleton belongs to the Hilbert lattice. $)
  spansnch $p |- ( A e. ~H -> ( span ` { A } ) e. CH ) $=
    ( chil wcel csn cspn cfv cort cch spansn wss snssi occl choccl 3syl eqeltrd
    ) ABCZADZEFQGFZGFZHAIPQBJRHCSHCABKQLRMNO $.
    $( [9-Jun-2004] $)

  $( The span of a Hilbert space singleton is a subspace. $)
  spansnsh $p |- ( A e. ~H -> ( span ` { A } ) e. SH ) $=
    ( chil wcel csn cspn cfv cch csh spansnch chsh syl ) ABCADEFZGCLHCAILJK $.
    $( [17-Dec-2004] $)

  ${
    spansnch.1 $e |- A e. ~H $.
    $( The span of a singleton in Hilbert space is a closed subspace. $)
    spansnchi $p |- ( span ` { A } ) e. CH $=
      ( chil wcel csn cspn cfv cch spansnch ax-mp ) ACDAEFGHDBAIJ $.
      $( [3-Jun-2004] $)
  $}

  $( A vector belongs to the span of its singleton. $)
  spansnid $p |- ( A e. ~H -> A e. ( span ` { A } ) ) $=
    ( chil wcel csn cort cfv cspn h1did spansn eleqtrrd ) ABCAADZEFEFKGFAHAIJ
    $.
    $( [3-Jun-2004] $)

  $( A scalar product with a vector belongs to the span of its singleton. $)
  spansnmul $p |- ( ( A e. ~H /\ B e. CC ) ->
                  ( B .h A ) e. ( span ` { A } ) ) $=
    ( cc wcel chil csm co csn cspn cfv csh spansnsh spansnid jca shmulcl 3com12
    wa 3expb sylan2 ancoms ) BCDZAEDZBAFGAHIJZDZUBUAUCKDZAUCDZQUDUBUEUFALAMNUAU
    EUFUDUEUAUFUDBAUCOPRST $.
    $( [3-Jun-2004] $)

  $( A member of a span of a singleton is a vector. $)
  elspansncl $p |- ( ( A e. ~H /\ B e. ( span ` { A } ) ) -> B e. ~H ) $=
    ( chil wcel csn wss cspn cfv snssi elspancl sylan ) ACDAEZCFBLGHDBCDACILBJK
    $.
    $( [17-Dec-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Membership in the span of a singleton. $)
    elspansn $p |- ( A e. ~H ->
                   ( B e. ( span ` { A } ) <-> E. x e. CC B = ( x .h A ) ) ) $=
      ( chil wcel csn cspn cfv cv csm co wceq cc wrex wb c0v sneq fveq2d eleq2d
      cif opreq2 eqeq2d rexbidv bibi12d ax-hv0cl elimel elspansni dedth ) BDEZC
      BFZGHZEZCAIZBJKZLZAMNZOCUIBPTZFZGHZEZCUMUQJKZLZAMNZOBPBUQLZULUTUPVCVDUKUS
      CVDUJURGBUQQRSVDUOVBAMVDUNVACBUQUMJUAUBUCUDAUQCBPDUEUFUGUH $.
      $( [5-Jun-2004] $)
  $}

  $( Membership in the span of a singleton.  All members are collinear with the
     generating vector. $)
  elspansn2 $p |- ( ( A e. ~H /\ B e. ~H /\ B =/= 0h ) ->
  ( A e. ( span ` { B } ) <-> A = ( ( ( A .ih B ) / ( B .ih B ) ) .h B ) ) ) $=
    ( chil wcel c0v wne csn cfv cort csp co cdiv csm wb eleq2d wi cif id opreq1
    wceq cspn spansn 3ad2ant2 eleq1 opreq1d eqeq12d bibi12d imbi2d neeq1 fveq2d
    w3a sneq opreq2 eqtrd opreq12d eqeq2d imbi12d elimel h1de2bi dedth2h 3impia
    ax-hv0cl bitrd ) ACDZBCDZBEFZUKABGZUAHZDZAVGIHZIHZDZAABJKZBBJKZLKZBMKZTZVEV
    DVIVLNVFVEVHVKABUBOUCVDVEVFVLVQNZVDVEVFVRPVFVDAEQZVKDZVSVSBJKZVNLKZBMKZTZNZ
    PVEBEQZEFZVSWFGZIHZIHZDZVSVSWFJKZWFWFJKZLKZWFMKZTZNZPABEEAVSTZVRWEVFWRVLVTV
    QWDAVSVKUDWRAVSVPWCWRRWRVOWBBMWRVMWAVNLAVSBJSUEUEUFUGUHBWFTZVFWGWEWQBWFEUIW
    SVTWKWDWPWSVKWJVSWSVJWIIWSVGWHIBWFULUJUJOWSWCWOVSWSWBWNBWFMWSWAWLVNWMLBWFVS
    JUMWSVNWFBJKWMBWFBJSBWFWFJUMUNUOWSRUOUPUGUQVSWFAECVBURBECVBURUSUTVAVC $.
    $( [5-Jun-2004] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x C $.
    $( The singletons of collinear vectors have the same span. $)
    spansncol $p |- ( ( A e. ~H /\ B e. CC /\ B =/= 0 ) ->
                    ( span ` { ( B .h A ) } ) = ( span ` { A } ) ) $=
      ( vx vy vz chil wcel cc csm co csn cspn cfv cv wceq wrex wi cmul eqeq2d
      wa cc0 wne ax-hvmulass 3com13 3expa biimprd ancoms adantll jctild rcla4ev
      w3a mulcl opreq1 syl6 rexlimdva 3adant3 divcl 3expb adantlr simprl simplr
      cdiv syl3anc divcan1 3exp imp32 opreq1d eqtr3d exp43 3imp rexlimdv impbid
      com4l wb hvmulcl elspansn syl 3ad2ant1 3bitr4d eqrdv ) AFGZBHGZBUAUBZUKZC
      BAIJZKLMZAKLMZWDCNZDNZWEIJZOZDHPZWHENZAIJZOZEHPZWHWFGZWHWGGZWDWLWPWAWBWLW
      PQWCWAWBTZWKWPDHWSWIHGZTZWKWIBRJZHGZWHXBAIJZOZTWPXAWKXEXCXAXEWKXAXDWJWHWA
      WBWTXDWJOZWTWBWAXFWIBAUCUDUESUFWBWTXCWAWTWBXCWIBULUGUHUIWOXEEXBHWMXBOWNXD
      WHWMXBAIUMSUJUNUOUPWDWOWLEHWAWBWCWMHGZWOWLQZQXGWAWBWCXHXGWAWBWCXHXGWATZWB
      WCTZTZWOWMBVBJZHGZWHXLWEIJZOZTWLXKWOXOXMXKXOWOXKXNWNWHXKXLBRJZAIJZXNWNXKX
      MWBWAXQXNOXGXJXMWAXGWBWCXMWMBUQURUSZXIWBWCUTXGWAXJVAXLBAUCVCXKXPWMAIXGXJX
      PWMOZWAXGWBWCXSXGWBWCXSWMBVDVEVFUSVGVHSUFXRUIWKXODXLHWIXLOWJXNWHWIXLWEIUM
      SUJUNVIVMVJVKVLWAWBWQWLVNZWCWSWEFGZXTWBWAYABAVOUGDWEWHVPVQUPWAWBWRWPVNWCE
      AWHVPVRVSVT $.
      $( [6-Jun-2004] $)
  $}

  $( Membership relation implied by equality of spans. $)
  spansneleqi $p |- ( A e. ~H ->
        ( ( span ` { A } ) = ( span ` { B } ) -> A e. ( span ` { B } ) ) ) $=
    ( chil wcel csn cspn cfv wceq spansnid eleq2 syl5ibcom ) ACDAAEFGZDLBEFGZHA
    MDAILMAJK $.
    $( [6-Jun-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Membership relation that implies equality of spans. $)
    spansneleq $p |- ( ( B e. ~H /\ A =/= 0h ) ->
         ( A e. ( span ` { B } ) -> ( span ` { A } ) = ( span ` { B } ) ) ) $=
      ( vx chil wcel c0v wne wa csn cspn cfv csm co wceq cc adantr wi cc0 com23
      ex cv wrex elspansn sneq fveq2d ad2antll opreq1 ax-hvmul0 sylan9eqr eqeq1
      wb biimprd sylan9 necon3d imp3a spansncol syld exp4b imp43 eqtrd rexlimdv
      3expia exp32 sylbid ) BDEZAFGZHZABIJKZEZACUAZBLMZNZCOUBZAIZJKZVHNZVEVIVMU
      KVFCBAUCPVGVLVPCOVGVJOEZVLVPVGVQVLHHVOVKIZJKZVHVLVOVSNVGVQVLVNVRJAVKUDUEU
      FVEVFVQVLVSVHNZVEVQVFVLVTQVEVQVFVLVTVEVQHVFVLHZVJRGZVTVEWAWBQVQVEVFVLWBVE
      VLVFWBVEVLVFWBQVEVLHVJRAFVEVJRNZVKFNZVLAFNZVEWCWDWCVEVKRBLMFVJRBLUGBUHUIT
      VLWEWDAVKFUJULUMUNTSUOPVEVQWBVTBVJUPVBUQURSUSUTVCVAVD $.
      $( [24-Jun-2006] $) $( [6-Jun-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The span of the singleton of an element of a subspace is included in the
       subspace. $)
    spansnss $p |- ( ( A e. SH /\ B e. A ) -> ( span ` { B } ) C_ A ) $=
      ( vx vy csh wcel wa csn cspn cfv cv csm co wceq cc wrex chil wb syl wi
      shel elspansn w3a shmulcl eleq1a 3exp com23 imp rexlimdv sylbid ssrdv ) A
      EFZBAFZGZCBHIJZAUNCKZUOFZUPDKZBLMZNZDOPZUPAFZUNBQFUQVARBAUADBUPUBSUNUTVBD
      OULUMUROFZUTVBTZTULVCUMVDULVCUMVDULVCUMUCUSAFVDURBAUDUSAUPUESUFUGUHUIUJUK
      $.
      $( [5-Jun-2004] $)
  $}

  $( A member of the span of the singleton of a vector is a member of a
     subspace containing the vector. $)
  elspansn3 $p |- ( ( A e. SH /\ B e. A /\ C e. ( span ` { B } ) ) ->
                   C e. A ) $=
    ( csh wcel csn cspn cfv wa spansnss sseld 3impia ) ADEZBAEZCBFGHZECAEMNIOAC
    ABJKL $.
    $( [19-Jun-2006] $) $( [16-Dec-2004] $)

  $( A span membership condition implying two vectors belong to the same
     subspace. $)
  elspansn4 $p |- ( ( ( A e. SH /\ B e. ~H ) /\
         ( C e. ( span ` { B } ) /\ C =/= 0h ) ) -> ( B e. A <-> C e. A ) ) $=
    ( csh wcel chil wa csn cspn cfv c0v wne wi elspansn3 3exp ad2ant2r spansnid
    com23 imp ad2antrr wceq spansneleq eleqtrrd 3expia exp4b com24 exp4a impbid
    syl5 imp43 ) ADEZBFEZGCBHIJZEZCKLZGGBAEZCAEZUKUNUPUQMZULUOUKUNURUKUPUNUQUKU
    PUNUQABCNORSPUKULUNUOUQUPMZUKUNULUOUSMUKUNULUOUSUKUQULUOGZUNUPUKUQUTUNUPUTU
    NGZBCHIJZEZUKUQGUPVABUMVBULBUMEUOUNBQTUTUNVBUMUACBUBSUCUKUQVCUPACBNUDUIUEUF
    UGRUJUH $.
    $( [27-Dec-2007] $) $( [17-Dec-2004] $)

  $( A vector belonging to both a subspace and the span of the singleton of a
     vector not in it must be zero. $)
  elspansn5 $p |- ( A e. SH -> ( ( ( B e. ~H /\ -. B e. A ) /\
         ( C e. ( span ` { B } ) /\ C e. A ) ) -> C = 0h ) ) $=
    ( csh wcel chil wn csn cspn cfv wa c0v wi wne elspansn4 biimprd exp32 com34
    wceq imp32 necon1bd exp31 imp4c ) ADEZBFEZBAEZGZCBHIJEZCAEZKZCLSZUDUEUJUGUK
    UDUEUJUGUKMUDUEKZUJKUFCLULUHUICLNZUFMULUHUMUIUFULUHUMUIUFMULUHUMKKUFUIABCOP
    QRTUAUBRUC $.
    $( [17-Dec-2004] $)

  $( The span of the singleton of an element of a subspace is included in the
     subspace. $)
  spansnss2 $p |- ( ( A e. SH /\ B e. ~H ) ->
                   ( B e. A <-> ( span ` { B } ) C_ A ) ) $=
    ( csh wcel chil wa csn cspn cfv wi spansnss ex adantr spansnid ssel syl5com
    wss adantl impbid ) ACDZBEDZFBADZBGHIZAQZTUBUDJUATUBUDABKLMUAUDUBJTUABUCDUD
    UBBNUCABOPRS $.
    $( [16-Dec-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Cancellation-type law that "extracts" a vector ` A ` from its inner
       product with a proportional vector ` B ` . $)
    normcan $p |- ( ( B e. ~H /\ B =/= 0h /\ A e. ( span ` { B } ) ) ->
            ( ( ( A .ih B ) / ( ( normh ` B ) ^ 2 ) ) .h B ) = A ) $=
      ( vx chil wcel c0v wne cfv csp co cdiv wceq wa cc adantr syl3anc ad2antrr
      csm simpr cc0 csn cspn cno c2 cexp cv wrex wb elspansn cmul simpl ax-his3
      opreq1 sylan9eqr normsq opreq12d hicl anidms his6 necon3bid biimpar eqtrd
      adantllr divcan4 opreq1d eqtr4d exp31 rexlimdv sylbid 3impia ) BDEZBFGZAB
      UAUBHEZABIJZBUCHUDUEJZKJZBRJZALZVKVLMZVMACUFZBRJZLZCNUGZVRVKVMWCUHVLCBAUI
      OVSWBVRCNVSVTNEZWBVRVSWDMZWBMZVQWAAWFVPVTBRWFVPVTBBIJZUJJZWGKJZVTVKWDWBVP
      WILVLVKWDMZWBMVNWHVOWGKWBWJVNWABIJZWHAWABIUMWJWDVKVKWKWHLVKWDSVKWDUKZWLVT
      BBULPUNVKVOWGLWDWBBUOQUPVCWEWIVTLZWBWEWDWGNEZWGTGZWMVSWDSVKWNVLWDVKWNBBUQ
      URQVSWOWDVKWOVLVKWGTBFBUSUTVAOVTWGVDPOVBVEWEWBSVFVGVHVIVJ $.
      $( [18-Mar-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A projection on the span of a singleton.  (The proof ws shortened by
       Mario Carneiro, 15-Dec-2013.) $)
    pjspansn $p |- ( ( A e. ~H /\ B e. ~H /\ A =/= 0h ) -> ( ( proj ` ( span `
        { A } ) ) ` B ) = ( ( ( B .ih A ) / ( ( normh ` A ) ^ 2 ) ) .h A ) ) $=
      ( vy chil wcel cfv co wceq csp cdiv csm cch 3ad2ant1 wa syl2anc caddc cc0
      adantr sylan syl3anc c0v wne w3a csn cspn cpj cv cva cort cno c2 spansnch
      wrex cexp simp2 eqid mpbii simprd opreq1 ad2antll pjhcl choccl syl simpl1
      pjeq ax-his2 simp1 csh spansnsh spansnid simpr shocorth 3impib wb orthcom
      chel syldan mpbid opreq2d hicl addid1 3eqtrd adantrr eqtrd opreq1d simpl3
      cc axpjcl normcan eqtr2d expr rexlimdva mpd ) ADEZBDEZAUAUBZUCZBBAUDUEFZU
      FFFZCUGZUHGZHZCWRUIFZUMZWSBAIGZAUJFUKUNGZJGZAKGZHZWQWRLEZWOXDWNWOXJWPAULZ
      MZWNWOWPUOZXJWONZWSWREZXDXNWSWSHXOXDNWSUPCBWSWRVEUQUROWQXBXICXCWQWTXCEZXB
      XIWQXPXBNZNZXHWSAIGZXFJGZAKGZWSXRXGXTAKXRXEXSXFJXRXEXAAIGZXSXBXEYBHWQXPBX
      AAIUSUTWQXPYBXSHXBWQXPNZYBXSWTAIGZPGZXSQPGZXSYCWSDEZWTDEZWNYBYEHWQYGXPWQX
      JWOYGXLXMBWRVAORZWQXCLEZXPYHWNWOYJWPWNXJYJXKWRVBVCZMWTXCVPZSWNWOWPXPVDZWS
      WTAVFTYCYDQXSPWQWNXPYDQHZWNWOWPVGWNXPNZAWTIGQHZYNYOWRVHEZAWREZXPYPWNYQXPA
      VIRWNYRXPAVJRWNXPVKYQYRXPYPAWTWRVLVMTWNXPYHYPYNVNWNYJXPYHYKYLSAWTVOVQVRSV
      SYCXSWGEZYFXSHYCYGWNYSYIYMWSAVTOXSWAVCWBWCWDWEWEXRWNWPXOYAWSHWNWOWPXQVDWN
      WOWPXQWFWQXOXQWQXJWOXOXLXMBWRWHORWSAWITWJWKWLWM $.
      $( [15-Dec-2013] $) $( [28-May-2006] $)
  $}

  ${
    spansnpj.1 $e |- A C_ ~H $.
    spansnpj.2 $e |- B e. ~H $.
    $( A subset of Hilbert space is orthogonal to the span of the singleton of
       a projection onto its orthocomplement. $)
    spansnpji $p |- A C_
                 ( _|_ ` ( span ` { ( ( proj ` ( _|_ ` A ) ) ` B ) } ) ) $=
      ( cort cfv cpj csn cspn chil ococss ax-mp occli chssii wcel pjclii spanss
      wss snssi mp2an csh wceq chshii sseqtri pjhclii spansnchi chsscon3i sstri
      spanid mpbi ) AAEFZEFZBUKGFFZHZIFZEFZAJRAULRCAKLUOUKRULUPRUOUKIFZUKUKJRUN
      UKRZUOUQRUKACMZNUMUKOURBUKUSDPUMUKSLUNUKQTUKUAOUQUKUBUKUSUCUKUILUDUOUKUMB
      UKUSDUEUFUSUGUJUH $.
      $( [4-Jun-2004] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.
    spanunsn.1 $e |- A e. CH $.
    spanunsn.2 $e |- B e. ~H $.
    $( The span of the union of a closed subspace with a singleton equals the
       span of its union with an orthogonal singleton. $)
    spanunsni $p |- ( span ` ( A u. { B } ) ) =
             ( span ` ( A u. { ( ( proj ` ( _|_ ` A ) ) ` B ) } ) ) $=
      ( vy vz vw vv vu cfv cph co cv wcel cva wceq wrex chil csm cc vx csn cspn
      cort cpj cun chshii wss csh snssi spancl mp2b shseli wi elspansni shmulcl
      wa pjclii mp3an13 shaddcl syl3an3 mp3an1 choccli pjhclii spansnmul adantl
      mpan pjpji opreq2i ax-hvdistr1 mp3an23 syl5eq opreq2d cheli hvmulcl mpan2
      ax-hvass 3expb syl2an eqtr4d rcla4eopr syl3anc sylibr opreq2 eqeq2d eleq1
      jca biimpa biimparc exp43 rexlimdv syl5bi rexlimiv sylbi cneg syl syl3an2
      negcl ancoms c0v hvm1neg hvnegid sylancl ax-hvcom syl2anc 3eqtr3d opreq1d
      hvaddcl hvaddid2 anim12i hvadd4 impbii eqriv chssii ax-mp spanuni opreq1i
      c1 spanid eqtri 3eqtr4i ) ABUBZUCJZKLZABAUDJZUEJJZUBZUCJZKLZAYBUFUCJZAYGU
      FUCJZUAYDYIUAMZYDNZYLYINZYMYLEMZFMZOLZPZFYCQZEAQYNEFAYCYLACUGZBRNZYBRUHZY
      CUINDBRUJZYBUKULZUMYSYNEAYOANZYRYNFYCYPYCNYPGMZBSLZPZGTQUUEYRYNUNZGBYPDUO
      UUEUUHUUIGTUUEUUFTNZUUHYRYNUUEUUJUQZYOUUGOLZYINZYLUULPZYNUUHYRUQUUKUULHMI
      MOLZPIYHQHAQZUUMUUKYOUUFBAUEJJZSLZOLZANZUUFYFSLZYHNZUULUUSUVAOLZPUUPAUINZ
      UUEUUJUUTYTUUJUVDUUEUURANZUUTUVDUUJUUQANZUVEYTBACDURZUUFUUQAUPUSYOUURAUTV
      AVBUUJUVBUUEYFRNZUUJUVBBYEACVCDVDZYFUUFVEVGVFUUKUULYOUURUVAOLZOLZUVCUUKUU
      GUVJYOOUUJUUGUVJPUUEUUJUUGUUFUUQYFOLZSLZUVJBUVLUUFSBACDVHVIUUJUUQRNZUVHUV
      MUVJPBACDVDZUVIUUFUUQYFVJVKVLVFZVMUUEYORNZUURRNZUVARNZUQUVCUVKPZUUJYOACVN
      ZUUJUVRUVSUUJUVNUVRUVOUUFUUQVOVPZUUJUVHUVSUVIUUFYFVOVPZWGUVQUVRUVSUVTYOUU
      RUVAVQVRVSVTHIAYHUUSUVAUULOWAWBHIAYHUULYTUVHYGRUHZYHUINUVIYFRUJZYGUKULZUM
      WCUUHYRUUNUUHYQUULYLYPUUGYOOWDWEWHUUNYNUUMYLUULYIWFWIVSWJWKWLWKWMWNYNYRFY
      HQZEAQYMEFAYHYLYTUWFUMUWGYMEAUUEYRYMFYHYPYHNYPUVAPZGTQUUEYRYMUNZGYFYPUVIU
      OUUEUWHUWIGTUUEUUJUWHYRYMUUKYOUVAOLZYDNZYLUWJPZYMUWHYRUQUUKUWJUUOPIYCQHAQ
      ZUWKUUKUUFWOZUUQSLZYOOLZANZUUGYCNZUWJUWPUUGOLZPUWMUUJUUEUWQUVDUUJUUEUWQYT
      UUJUVDUWOANZUUEUWQUUJUWNTNZUWTUUFWRZUVDUXAUVFUWTYTUVGUWNUUQAUPUSWPUWOYOAU
      TWQVBWSUUJUWRUUEUUAUUJUWRDBUUFVEVGVFUUKUWJUWPUVJOLZUWSUUKWTUWJOLZUWOUUROL
      ZUWJOLZUWJUXCUUKWTUXEUWJOUUJWTUXEPUUEUUJUURXRWOUURSLZOLZUURUWOOLZWTUXEUUJ
      UXGUWOUUROUUJUVNUXGUWOPUVOUUFUUQXAVPVMUUJUVRUXHWTPUWBUURXBWPUUJUVRUWORNZU
      XIUXEPUWBUUJUXAUVNUXJUXBUVOUWNUUQVOXCZUURUWOXDXEXFVFXGUUKUWJRNZUXDUWJPUUE
      UVQUVSUXLUUJUWAUWCYOUVAXHVSUWJXIWPUUKUXJUVRUQZUVQUVSUQUXFUXCPUUJUXMUUEUUJ
      UXJUVRUXKUWBWGVFUUEUVQUUJUVSUWAUWCXJUWOUURYOUVAXKXEXFUUKUUGUVJUWPOUVPVMVT
      HIAYCUWPUUGUWJOWAWBHIAYCUWJYTUUDUMWCUWHYRUWLUWHYQUWJYLYPUVAYOOWDWEWHUWLYM
      UWKYLUWJYDWFWIVSWJWKWLWKWMWNXLXMYJAUCJZYCKLYDAYBACXNZUUAUUBDUUCXOXPUXNAYC
      KUVDUXNAPYTAXSXOZXQXTYKUXNYHKLYIAYGUXOUVHUWDUVIUWEXOXPUXNAYHKUXPXQXTYA $.
      $( [3-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The span of a pair of vectors. $)
    spanpr $p |- ( ( A e. ~H /\ B e. ~H ) ->
                 ( span ` { ( A +h B ) } ) C_ ( span ` { A , B } ) ) $=
      ( vx chil wcel wa cva csn cspn cfv cph csh spansnsh syl2an adantr anim12i
      co spansnid wss snssi cpr cv shscl shsva simpr elspansn3 syl3anc ex ssrdv
      sylc cun df-pr fveq2i wceq spanun syl5req sseqtrd ) ADEZBDEZFZABGQZHIJZAH
      ZIJZBHZIJZKQZABUAZIJZUTCVBVGUTCUBZVBEZVJVGEZUTVKFVGLEZVAVGEZVKVLUTVMVKURV
      DLEZVFLEZVMUSAMZBMZVDVFUCNOUTVNVKUTVOVPFAVDEZBVFEZFVNURVOUSVPVQVRPURVSUSV
      TARBRPVDVFABUDUJOUTVKUEVGVAVJUFUGUHUIUTVIVCVEUKZIJZVGVHWAIABULUMURVCDSVED
      SWBVGUNUSADTBDTVCVEUONUPUQ $.
      $( [9-Jun-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    h1datom.1 $e |- A e. CH $.
    h1datom.2 $e |- B e. ~H $.
    $( A 1-dimensional subspace is an atom. $)
    h1datomi $p |- ( A C_ ( _|_ ` ( _|_ ` { B } ) ) ->
                  ( A = ( _|_ ` ( _|_ ` { B } ) ) \/ A = 0H ) ) $=
      ( vx vy cort cfv wss wne c0h wceq wi wa wcel c0v csm co cc cc0 wo cv wrex
      csn chne0i ssel h1de2ci opreq1 chil ax-hvmul0 ax-mp eqeq1 syl5ibr necon3d
      syl6eq adantl cdiv reccl csh chshii shmulcl mp3an1 syl adantr opreq2 cmul
      c1 ex simpl ax-hvmulass mp3an3 syl2anc recid2 eqtr3d ax-hvmulid sylan9eqr
      opreq1d eleq1d sylibd exp31 com23 syld com3r exp3a rexlimdv syl5bi sylcom
      snssi chssii occon2i ococi syl6sseq syl6 anc2li eqss syl6ibr necon1d neor
      imp sylibr ) ABUDZGHGHZIZAXBJAKLZMAXBLZXDUAXCAKAXBXCAKJZXCXBAIZNXEXCXFXGX
      CXFBAOZXGXFEUBZPJZEAUCXCXHEACUEXCXJXHEAXCXIAOZXIXBOZXJXHMZAXBXIUFXLXIFUBZ
      BQRZLZFSUCXKXMFXIBDUGXKXPXMFSXKXNSOZXPXMXQXPNZXJXKXHXRXJXNTJZXKXHMZXPXJXS
      MXQXPXNTXIPXNTLZXIPLXPXOPLYAXOTBQRZPXNTBQUHBUIOZYBPLDBUJUKUOXIXOPULUMUNUP
      XQXPXSXTMXQXSXPXTXQXSXPXTXQXSNZXPNZXKVGXNUQRZXIQRZAOZXHYDXKYHMZXPYDYFSOZY
      IXNURZYJXKYHAUSOYJXKYHACUTYFXIAVAVBVHVCVDYEYGBAXPYDYGYFXOQRZBXIXOYFQVEYDY
      LVGBQRZBYDYFXNVFRZBQRZYLYMYDYJXQYOYLLZYKXQXSVIYJXQYCYPDYFXNBVJVKVLYDYNVGB
      QXNVMVQVNYCYMBLDBVOUKUOVPVRVSVTWAWSWBWCWDWEWFWGWEWFXHXBAGHGHZAXHXAAIXBYQI
      BAWHXAAYCXAUIIDBUIWHUKACWIWJVCACWKWLWMWNAXBWOWPWQXDAXBWRWT $.
      $( [20-Jul-2001] $)
  $}

  $( A 1-dimensional subspace is an atom. $)
  h1datom $p |- ( ( A e. CH /\ B e. ~H ) ->
                ( A C_ ( _|_ ` ( _|_ ` { B } ) ) ->
                ( A = ( _|_ ` ( _|_ ` { B } ) ) \/ A = 0H ) ) ) $=
    ( cch wcel chil csn cort cfv wss wceq c0h wo wi cif c0v sseq1 eqeq1 imbi12d
    fveq2d elimel orbi12d sseq2d eqeq2d orbi1d h0elch ax-hv0cl h1datomi dedth2h
    sneq ) ACDZBEDZABFZGHZGHZIZAUNJZAKJZLZMUJAKNZUNIZUSUNJZUSKJZLZMUSUKBONZFZGH
    ZGHZIZUSVGJZVBLZMABKOAUSJZUOUTURVCAUSUNPVKUPVAUQVBAUSUNQAUSKQUARBVDJZUTVHVC
    VJVLUNVGUSVLUMVFGVLULVEGBVDUISSZUBVLVAVIVBVLUNVGUSVMUCUDRUSVDAKCUETBOEUFTUG
    UH $.
    $( [22-Jul-2001] $)

