$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Span (cont.) and one-dimensional subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The span of the singleton of the zero vector is the zero subspace. $)
  spansn0 $p |- ( span ` { 0h } ) = 0H $=
    ( c0h cspn cfv c0v csn df-ch0 fveq2i csh wcel wceq h0elsh spanid ax-mp
    eqtr3 ) ABCZDEZBCAAPBFGAHIOAJKALMN $.
    $( [17-Jan-2005] $) $( [14-Jan-2005] $)

  $( The span of the empty set is the zero subspace.  Remark 11.6.e of
     [Schechter] p. 276. $)
  span0 $p |- ( span ` (/) ) = 0H $=
    ( c0 cspn cfv c0h chil wss h0elsh shssi 0ss spanss mp2an csh wcel wceq
    spanid ax-mp sseqtr spanclt sh0let eqssi ) ABCZDUADBCZDDEFADFUAUBFDGHDIADJK
    DLMUBDNGDOPQUALMZDUAFAEFUCEIARPUASPT $.
    $( [17-Aug-2004] $) $( [3-Jun-2004] $)

  ${
    $d x A $.  $d x B $.
    elspan.1 $e |- B e. V $.
    $( Membership in the span of a subset of Hilbert space. $)
    elspan $p |- ( A (_ H~ ->
                ( B e. ( span ` A ) <-> A. x e. SH ( A (_ x -> B e. x ) ) ) $=
      ( chil wss cspn cfv wcel cv csh crab cint wi wral spanvalt eleq2d
      elintrab syl6bb ) BEFZCBGHZICBAJZFZAKLMZIUCCUBINAKOTUAUDCABPQUCACKDRS $.
      $( [3-Jun-2004] $) $( [2-Jun-2004] $)

  $}

  ${
    spanun.1 $e |- A (_ H~ $.
    spanun.2 $e |- B (_ H~ $.
    $d x y z w A $.  $d x y z w B $.
    $( The span of a union is the subspace sum of spans. $)
    spanun $p |- ( span ` ( A u. B ) ) = ( ( span ` A ) +H ( span ` B ) ) $=
      ( vx vz vw vy cun cspn cfv cph co chil wss csh wcel spanclt ax-mp shscl
      shssi spanss2 unss12 mp2an shunss sstri spanss wceq spanid sseqtr cv wa
      cva wex wrex shsel r2ex bitr wi wral r19.27av wb visset elspan anbi12i
      r19.26 bitr4 sylanb prth unss syl5ibr shaddcltOLD sylan9r eleq1 biimprd
      sylan9 exp42 imp4c r19.20i unssi sylibr syl 19.23aivv sylbi ssriv eqssi
      ) ABIZJKZAJKZBJKZLMZWHWKJKZWKWKNOWGWKOWHWLOWKWIWJANOZWIPQCARSZBNOZWJPQDBR
      SZTZUAWGWIWJIZWKAWIOZBWJOZWGWROWMWSCAUBSWOWTDBUBSAWIBWJUCUDWIWJWNWPUEUFWG
      WKUGUDWKPQWLWKUHWQWKUISUJEWKWHEUKZWKQZFUKZWIQZGUKZWJQZULZXAXCXEUMMZUHZULZ
      GUNFUNZXAWHQZXBXIGWJUOFWIUOXKFGWIWJXAWNWPUPXIFGWIWJUQURXJXLFGXJAHUKZOZXCX
      MQZUSZBXMOZXEXMQZUSZULZXIULZHPUTZXLXTHPUTZXIYBXGXTXIHPVAXGXPHPUTZXSHPUTZU
      LYCXDYDXFYEWMXDYDVBCHAXCFVCVDSWOXFYEVBDHBXEGVCVDSVEXPXSHPVFVGVHYBWGXMOZXA
      XMQZUSZHPUTZXLYAYHHPXMPQZXPXSXIYHYJXPXSXIYHYJXTULYFXHXMQZXIYGXTYFXOXRULZY
      JYKXTXNXQULYLYFXNXOXQXRVIABXMVJVKXCXEXMVLVMXIYGYKXAXHXMVNVOVPVQVRVSWGNOXL
      YIVBABNCDVTHWGXAEVCVDSWAWBWCWDWEWF $.
      $( [28-Jun-2004] $) $( [2-Jun-2004] $)
  $}

  $( The span of a union is the subspace sum of spans. $)
  spanunt $p |- ( ( A (_ H~ /\ B (_ H~ ) ->
                ( span ` ( A u. B ) ) = ( ( span ` A ) +H ( span ` B ) ) ) $=
    ( chil wss cun cspn cfv cph co wceq cif uneq1 fveq2d fveq2 opreq1d eqeq12d
    uneq2 opreq2d sseq1 ssid elimhyp spanun dedth2h ) ACDZBCDZABEZFGZAFGZBFGZHI
    ZJUDACKZBEZFGZUKFGZUIHIZJUKUEBCKZEZFGZUNUPFGZHIZJABCCAUKJZUGUMUJUOVAUFULFAU
    KBLMVAUHUNUIHAUKFNOPBUPJZUMURUOUTVBULUQFBUPUKQMVBUIUSUNHBUPFNRPUKUPUDUKCDCC
    DZACAUKCSCUKCSCTZUAUEUPCDVCBCBUPCSCUPCSVDUAUBUC $.
    $( [18-Jun-2006] $) $( [9-Jun-2006] $)

  ${
    sshjococ.1 $e |- A (_ H~ $.
    sshjococ.2 $e |- B (_ H~ $.
    $( The join of two Hilbert space subsets (not necessarily closed subspaces)
       equals the join of their closures (double orthocomplements). $)
    sshhococ $p |- ( A vH B ) =
                 ( ( _|_ ` ( _|_ ` A ) ) vH ( _|_ ` ( _|_ ` B ) ) ) $=
      ( chj co cort cfv cun wss chil ococss ax-mp unss12 mp2an unssi occl
      choccl chssi occon2 wceq sshjvalt chjval 3sstr4 ssun1 sstri sseqtr4 cch
      wcel sshjclt ssun2 chlubi ococ sseqtr eqssi ) ABEFZAGHZGHZBGHZGHZEFZABIZG
      HGHZURUTIZGHGHZUPVAVBVDJZVCVEJAURJZBUTJZVFAKJZVGCALMBKJZVHDBLMAURBUTNOVBV
      DABKCDPZURUTKURUQACQRZSUTUSBDQRZSPTMVIVJUPVCUACDABUBOZURUTVLVMUCUDVAUPGHZ
      GHZUPURVPJZUTVPJZVAVPJAUPJVQAVCUPAVBVCABUEVBKJVBVCJVKVBLMZUFVNUGAUPCUPVIV
      JUPUHUICDABUJOZSZTMBUPJVRBVCUPBVBVCBAUKVSUFVNUGBUPDWATMURUTVPVLVMVOUPVTRR
      ULOUPVTUMUNUO $.
      $( [2-Sep-2004] $) $( [1-Jun-2004] $)
  $}

  $( Hilbert space has a nonzero vector iff it is not trivial. $)
  hne0 $p |- ( H~ =/= 0H <-> E. x e. H~ x =/= 0h ) $=
    ( chil helch chne0 ) ABCD $.
    $( [19-Mar-2006] $) $( [24-Feb-2006] $)

  $( The supremum of the empty set. $)
  chsup0 $p |- ( \/H ` (/) ) = 0H $=
    ( c0 chsup cfv c0h wss wceq csn 0ss cch wi wcel h0elch snssi ax-mp chsupss
    mp2an chsupsn sseqtr chsupclt chle0 mpbi ) ABCZDEUBDFUBDGZBCZDAUCEZUBUDEZUC
    HAIEZUCIEZUEUFJIHZDIKZUHLDIMNAUCOPNUJUDDFLDQNRUBUGUBIKUIASNTUA $.
    $( [13-Aug-2002] $)

  ${
    $d x A $.  $d x B $.
    h1deot.1 $e |- B e. H~ $.
    $( Membership in orthocomplement of 1-dimensional subspace. $)
    h1deot $p |- ( A e. ( _|_ ` { B } ) <->
                 ( A e. H~ /\ ( A .ih B ) = 0 ) ) $=
      ( vx csn cort cfv wcel chil cv csp co cc0 wceq wral wa wss wb snssi
      ax-mp ocelt wi wal df-ral elsn imbi1i albii elisseti opreq2 eqeq1d
      ceqsalv 3bitr anbi2i bitr ) ABEZFGHZAIHZADJZKLZMNZDUOOZPZUQABKLZMNZPUOIQZ
      UPVBRBIHVECBISTDAUOUATVAVDUQVAURUOHZUTUBZDUCURBNZUTUBZDUCVDUTDUOUDVGVIDVF
      VHUTDBUEUFUGUTVDDBBICUHVHUSVCMURBAKUIUJUKULUMUN $.
      $( [7-Jul-2001] $)

    $( Membership in 1-dimensional subspace. $)
    h1det $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
    ( A e. H~ /\ A. x e. H~ ( ( B .ih x ) = 0 -> ( A .ih x ) = 0 ) ) ) $=
      ( csn cort cfv wcel chil cv csp co cc0 wceq wral wa wi wss wb snssi
      ax-mp occl chssi ocelt h1deot orthcom mpan2 pm5.32i bitr imbi1i impexp
      ralbii2 anbi2i ) BCEZFGZFGHZBIHZBAJZKLMNZAUOOZPZUQCURKLMNZUSQZAIOZPUOIRUP
      VASUOUNCIHZUNIRDCITUAUBUCABUOUDUAUTVDUQUSVCAUOIURUOHZUSQURIHZVBPZUSQVGVCQ
      VFVHUSVFVGURCKLMNZPVHURCDUEVGVIVBVGVEVIVBSDURCUFUGUHUIUJVGVBUSUKUIULUMUI
      $.
      $( [7-Jul-2001] $)
  $}

  $( A generating vector belongs to the 1-dimensional subspace it
     generates. $)
  h1did $p |- ( A e. H~ -> A e. ( _|_ ` ( _|_ ` { A } ) ) ) $=
    ( chil wcel csn cort cfv wss snssi ococss syl snssg mpbird ) ABCZAADZEFEFZC
    NOGZMNBGPABHNIJAOBKL $.
    $( [22-Jul-2001] $)

  $( A non-zero vector generates a (non-zero) 1-dimensional subspace. $)
  h1dn0 $p |- ( ( A e. H~ /\ A =/= 0h ) ->
              ( _|_ ` ( _|_ ` { A } ) ) =/= 0H ) $=
    ( chil wcel c0v wne csn cort cfv c0h wceq eleq2 h1did syl5cbi elch0 syl6ib
    necon3d imp ) ABCZADEAFGHGHZIERSIADRSIJZAICZADJTASCUARSIAKALMANOPQ $.
    $( [19-Jun-2006] $) $( [22-Jul-2001] $)

  ${
    $d x A $.  $d x B $.
    h1de2.1 $e |- A e. H~ $.
    h1de2.2 $e |- B e. H~ $.
    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2 $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) ->
                ( ( B .ih B ) .h A ) = ( ( A .ih B ) .h B ) ) $=
      ( vx csn cort cfv wcel csp co cmul wceq csm cmin cc0 cmv chil hicl
      hvmulcl his2subt mp3an mulcom cc ax-his3 3eqtr4 subeq0 mpbir eqtr cv wi
      wral h1det mpbiran hvsubcl opreq2 eqeq1d imbi12d rcla4v ax-mp sylbi wb
      orthcom mp2an 3imtr4g mpi opreq12i eqtr2 syl5eq mulcl sylib eqcomd bcseq
      ) ABFGHGHIZABJKZBAJKZLKZAAJKZBBJKZLKZMVSANKZVOBNKZMVNVTVQVNVTVQOKZPMVTVQM
      VNWAWBQKZAJKZPWCVNWDBJKZPMZWEPMZWFWABJKZWBBJKZOKZPWARIZWBRIZBRIZWFWKMVSAB
      BDDSZCTZVOBABCDSZDTZDWAWBBUAUBWKPMWIWJMVSVOLKZVOVSLKZWIWJVSVOWOWQUCVSUDIZ
      ARIZWNWIWSMWOCDVSABUEUBVOUDIZWNWNWJWTMWQDDVOBBUEUBUFWIWJWABWPDSWBBWRDSUGU
      HUIVNBWDJKZPMZAWDJKZPMZWGWHVNBEUJZJKZPMZAXHJKZPMZUKZERULZXEXGUKZVNXBXNEAB
      DUMCUNWDRIZXNXOUKWAWBWPWRUOZXMXOEWDRXHWDMZXJXEXLXGXRXIXDPXHWDBJUPUQXRXKXF
      PXHWDAJUPUQURUSUTVAXPWNWGXEVBXQDWDBVCVDXPXBWHXGVBXQCWDAVCVDVEVFWEWAAJKZWB
      AJKZOKZWCWLWMXBWEYAMWPWRCWAWBAUAUBXSVTXTVQOXSVSVRLKZVTXAXBXBXSYBMWOCCVSAA
      UEUBVSVRWOAACCSZUCUIXCWNXBXTVQMWQDCVOBAUEUBVGVHVIVTVQVRVSYCWOVJVOVPWQBADC
      SVJUGVKVLABCDVMVK $.
      $( [17-Jul-2001] $)

    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2b $p |- ( B =/= 0h -> ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                A = ( ( ( A .ih B ) / ( B .ih B ) ) .h B ) ) ) $=
      ( c0v wne csp co cc0 csn cort cfv wcel cdiv csm wceq wb chil his6t ax-mp
      necon3bii wa c1 h1de2 adantl opreq2d cmul cc hicl recclz ax-hvmulass
      mp3an23 syl ax1cn divcan1z opreq1d eqtr3d ax-hvmulid syl6eq adantr
      axmulcom mpan2 divrecz eqtr4d 3eqtr3d ex eleq1 divclz h1did csh wi wss
      snssi occl choccl chshi shmulcltOLD syl5cbir impbid sylbir ) BEFBBGHZIFZA
      BJZKLZKLZMZAABGHZWANHZBOHZPZQWAIBEBRMZWAIPBEPQDBSTUAWBWFWJWBWFWJWBWFUBZUC
      WANHZWAAOHZOHZWMWGBOHZOHZAWIWLWNWPWMOWFWNWPPWBABCDUDUEUFWBWOAPWFWBWOUCAOH
      ZAWBWMWAUGHZAOHZWOWRWBWMUHMZWTWOPZWABBDDUIZUJZXAWAUHMARMZXBXCCWMWAAUKULUM
      WBWSUCAOWAUCXCUNUOUPUQXEWRAPCAURTUSUTWBWQWIPWFWBWMWGUGHZBOHZWQWIWBXAXGWQP
      ZXDXAWGUHMZWKXHABCDUIZDWMWGBUKULUMWBXFWHBOWBXFWGWMUGHZWHWBXAXFXKPZXDXAXIX
      LXJWMWGVAVBUMWGWAXJXCVCVDUPUQUTVEVFWJWFWIWEMZWBAWIWEVGWBWHUHMZXMWGWAXJXCV
      HXNBWEMZXMWKXODBVITWEVJMXNXOUBXMVKWEWDWCWKWCRVLDBRVMTVNVOVPWHBWEVQTVBUMVR
      VSVT $.
      $( [19-Jul-2001] $)

    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2bOLD $p |- ( -. B = 0h -> ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                A = ( ( ( A .ih B ) / ( B .ih B ) ) .h B ) ) ) $=
      ( c0v wceq wn csp co cc0 wne csn cort cfv wcel cdiv csm wb chil his6t
      ax-mp necon3abii wa c1 h1de2 adantl opreq2d cmul cc hicl recclz
      ax-hvmulass mp3an23 syl ax1cn divcan1z opreq1d eqtr3d ax-hvmulid syl6eq
      adantr axmulcom mpan2 divrecz eqtr4d 3eqtr3d ex eleq1 divclz h1did csh
      wi wss snssi occl choccl chshi shmulcltOLD syl5cbir impbid sylbir ) BEFZG
      BBHIZJKZABLZMNZMNZOZAABHIZWCPIZBQIZFZRWBWCJBSOZWCJFWBRDBTUAUBWDWHWLWDWHWL
      WDWHUCZUDWCPIZWCAQIZQIZWOWIBQIZQIZAWKWNWPWRWOQWHWPWRFWDABCDUEUFUGWDWQAFWH
      WDWQUDAQIZAWDWOWCUHIZAQIZWQWTWDWOUIOZXBWQFZWCBBDDUJZUKZXCWCUIOASOZXDXECWO
      WCAULUMUNWDXAUDAQWCUDXEUOUPUQURXGWTAFCAUSUAUTVAWDWSWKFWHWDWOWIUHIZBQIZWSW
      KWDXCXIWSFZXFXCWIUIOZWMXJABCDUJZDWOWIBULUMUNWDXHWJBQWDXHWIWOUHIZWJWDXCXHX
      MFZXFXCXKXNXLWOWIVBVCUNWIWCXLXEVDVEUQURVAVFVGWLWHWKWGOZWDAWKWGVHWDWJUIOZX
      OWIWCXLXEVIXPBWGOZXOWMXQDBVJUAWGVKOXPXQUCXOVLWGWFWEWMWESVMDBSVNUAVOVPVQWJ
      BWGVRUAVCUNVSVTWA $.
      $( [19-Jul-2001] $)

    $( Lemma for ~ h1de2ct . $)
    h1de2ctlem $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                 E. x e. CC A = ( x .h B ) ) $=
      ( csn cort cfv wcel cv csm co wceq cc wrex c0v wi cc0 sneq fveq2d eleq2d
      chil elisseti elsnc hsn0elch ococ eleq2i ax-hvmul0 ax-mp eqeq2i 3bitr4r
      syl6rbbr 0cn opreq1 eqeq2d rcla4ev mpan syl6bir wn csp cdiv h1de2bOLD
      wne wb his6t necon3abii hicl divclz sylbir sylan ex sylbid pm2.61i eleq1
      h1did csh wa wss snssi occl choccl chshi shmulcltOLD mpan2 syl5cbir
      r19.23aiv impbi ) BCFZGHZGHZIZBAJZCKLZMZANOZCPMZWKWOQWPWKBRCKLZMZWOWPWKBP
      FZGHZGHZIZWRWPWJXABWPWIWTGWPWHWSGCPSTTUABWSIBPMXBWRBPBUBDUCUDXAWSBWSUEUFU
      GWQPBCUBIZWQPMECUHUIUJUKULRNIWRWOUMWNWRARNWLRMWMWQBWLRCKUNUOUPUQURWPUSZWK
      BBCUTLZCCUTLZVALZCKLZMZWOBCDEVBXDXIWOXGNIZXIWOXDWNXIAXGNWLXGMWMXHBWLXGCKU
      NUOUPXDXFRVCXJWPXFRXCXFRMWPVDECVEUIVFXEXFBCDEVGCCEEVGVHVIVJVKVLVMWNWKANWN
      WKWMWJIZWLNIZBWMWJVNXLCWJIZXKXCXMECVOUIWJVPIXLXMVQXKQWJWIWHXCWHUBVRECUBVS
      UIVTWAWBWLCWJWCUIWDWEWFWG $.
      $( [19-Jul-2001] $)
  $}

  ${
    $d x A $.  $d x B $.
    h1de2ct.1 $e |- B e. H~ $.
    $( Membership in 1-dimensional subspace.  All members are collinear with
       the generating vector. $)
    h1de2ct $p |- ( A e. ( _|_ ` ( _|_ ` { B } ) ) <->
                 E. x e. CC A = ( x .h B ) ) $=
      ( csn cort cfv wcel chil cv csm co wceq cc wrex wss snssi ax-mp occl
      choccl chel eleq1 hvmulclt mpan2 syl5cbir r19.23aiv wb c0v cif eqeq1
      rexbidv bibi12d ax-hv0cl elimel h1de2ctlem dedth pm5.21nii ) BCEZFGZFGZHZ
      BIHZBAJZCKLZMZANOZBUTUSURCIHZURIPDCIQRSTUAVEVBANVEVBVDIHZVCNHZBVDIUBVIVGV
      HDVCCUCUDUEUFVBVAVFUGVBBUHUIZUTHZVJVDMZANOZUGBUHBVJMZVAVKVFVMBVJUTUBVNVEV
      LANBVJVDUJUKULAVJCBUHIUMUNDUOUPUQ $.
      $( [21-Jul-2001] $)
  $}

  ${
    $d x y z A $.  $d x B $.
    spansn.1 $e |- A e. H~ $.
    $( The span of a singleton in Hilbert space equals its closure. $)
    spansn $p |- ( span ` { A } ) = ( _|_ ` ( _|_ ` { A } ) ) $=
      ( vx vy vz csn cspn cfv cort chil wss wcel snssi ax-mp spanssoc cv wi
      csh wral wa csm co wceq cc wrex h1de2ct shmulcltOLD eleq1a com12 syl9r
      exp4a com3r r19.23aiv sylbi imp elisseti snss syl5ibr r19.21aiva wb
      visset elspan sylibr ssriv eqssi ) AFZGHZVFIHIHZVFJKZVGVHKAJLVIBAJMNZVFON
      CVHVGCPZVHLZVFDPZKZVKVMLZQZDRSZVKVGLZVLVPDRVLVMRLZTAVMLZVOVNVLVSVTVOQZVLV
      KEPZAUAUBZUCZEUDUEVSWAQZEVKABUFWDWEEUDWDVSWBUDLZWAWDVSWFVTVOVSWFVTTWCVMLZ
      WDVOWBAVMUGWGWDVOWCVMVKUHUIUJUKULUMUNUOAVMAJBUPUQURUSVIVRVQUTVJDVFVKCVAVB
      NVCVDVE $.
      $( [28-Jun-2004] $) $( [3-Jun-2004] $)

    $( Membership in the span of a singleton. $)
    elspansn $p |- ( B e. ( span ` { A } ) <-> E. x e. CC B = ( x .h A ) ) $=
      ( csn cspn cfv wcel cort cv csm co wceq cc wrex spansn eleq2i h1de2ct
      bitr ) CBEZFGZHCTIGIGZHCAJBKLMANOUAUBCBDPQACBDRS $.
      $( [28-Jun-2004] $) $( [3-Jun-2004] $)
  $}

  $( The span of a singleton in Hilbert space equals its closure. $)
  spansnt $p |- ( A e. H~ -> ( span ` { A } ) = ( _|_ ` ( _|_ ` { A } ) ) ) $=
    ( chil wcel csn cspn cfv cort wceq c0v cif sneq fveq2d eqeq12d ax-hv0cl
    elimel spansn dedth ) ABCZADZEFZSGFZGFZHRAIJZDZEFZUDGFZGFZHAIAUCHZTUEUBUGUH
    SUDEAUCKZLUHUAUFGUHSUDGUILLMUCAIBNOPQ $.
    $( [28-Jun-2004] $) $( [4-Jun-2004] $)

  $( The span of a Hilbert space singleton belongs to the Hilbert lattice. $)
  spansncht $p |- ( A e. H~ -> ( span ` { A } ) e. CH ) $=
    ( chil wcel csn cspn cfv cort cch spansnt wss snssi occlt chocclt 3syl
    eqeltrd ) ABCZADZEFQGFZGFZHAIPQBJRHCSHCABKQLRMNO $.
    $( [28-Jun-2004] $) $( [9-Jun-2004] $)

  $( The span of a Hilbert space singleton is a subspace. $)
  spansnsht $p |- ( A e. H~ -> ( span ` { A } ) e. SH ) $=
    ( chil wcel csn cspn cfv cch csh spansncht chsh syl ) ABCADEFZGCLHCAILJK $.
    $( [21-Dec-2004] $) $( [17-Dec-2004] $)

  ${
    spansnch.1 $e |- A e. H~ $.
    $( The span of a singleton in Hilbert space is a closed subspace. $)
    spansnch $p |- ( span ` { A } ) e. CH $=
      ( chil wcel csn cspn cfv cch spansncht ax-mp ) ACDAEFGHDBAIJ $.
      $( [29-Jun-2004] $) $( [3-Jun-2004] $)
  $}

  $( A vector belongs to the span of its singleton. $)
  spansnid $p |- ( A e. H~ -> A e. ( span ` { A } ) ) $=
    ( chil wcel csn cort cfv cspn h1did spansnt eleqtrrd ) ABCAADZEFEFKGFAHAIJ
    $.
    $( [29-Jun-2004] $) $( [3-Jun-2004] $)

  $( A scalar product with a vector belongs to the span of its singleton. $)
  spansnmul $p |- ( ( A e. H~ /\ B e. CC ) ->
                  ( B .h A ) e. ( span ` { A } ) ) $=
    ( cc wcel chil csm co csn cspn cfv csh wa shmulcltOLD imp an1s spansnsht
    spansnid jca sylan2 ancoms ) BCDZAEDZBAFGAHIJZDZUAUCKDZAUCDZLUDUBUEUAUFUDUE
    UAUFLUDBAUCMNOUBUEUFAPAQRST $.
    $( [29-Jun-2004] $) $( [3-Jun-2004] $)

  $( A member of a span of a singleton is a vector. $)
  elspansnclt $p |- ( ( A e. H~ /\ B e. ( span ` { A } ) ) -> B e. H~ ) $=
    ( csn chil wss cspn cfv wcel elspanclt snssi sylan ) ACZDEBLFGHBDHADHLBIADJ
    K $.
    $( [22-Dec-2004] $) $( [17-Dec-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Membership in the span of a singleton. $)
    elspansnt $p |- ( A e. H~ ->
                   ( B e. ( span ` { A } ) <-> E. x e. CC B = ( x .h A ) ) ) $=
      ( chil wcel csn cspn cfv cv csm co wceq cc wrex wb c0v cif sneq fveq2d
      eleq2d opreq2 eqeq2d rexbidv bibi12d ax-hv0cl elimel elspansn dedth ) BDE
      ZCBFZGHZEZCAIZBJKZLZAMNZOCUIBPQZFZGHZEZCUMUQJKZLZAMNZOBPBUQLZULUTUPVCVDUK
      USCVDUJURGBUQRSTVDUOVBAMVDUNVACBUQUMJUAUBUCUDAUQCBPDUEUFUGUH $.
      $( [29-Jun-2004] $) $( [5-Jun-2004] $)
  $}

  $( Membership in the span of a singleton.  All members are collinear with
     the generating vector. $)
  elspansn2t $p |- ( ( A e. H~ /\ B e. H~ /\ B =/= 0h ) ->
  ( A e. ( span ` { B } ) <-> A = ( ( ( A .ih B ) / ( B .ih B ) ) .h B ) ) ) $=
    ( chil wcel c0v wne w3a csn cspn cfv cort csp co cdiv csm wceq wb spansnt
    eleq2d 3ad2ant2 wi cif eleq1 id opreq1 opreq1d eqeq12d bibi12d imbi2d
    neeq1 sneq fveq2d opreq2 eqtrd opreq12d eqeq2d imbi12d ax-hv0cl elimel
    h1de2b dedth2h 3impia bitrd ) ACDZBCDZBEFZGABHZIJZDZAVGKJZKJZDZAABLMZBBLMZN
    MZBOMZPZVEVDVIVLQVFVEVHVKABRSTVDVEVFVLVQQZVDVEVFVRUAVFVDAEUBZVKDZVSVSBLMZVN
    NMZBOMZPZQZUAVEBEUBZEFZVSWFHZKJZKJZDZVSVSWFLMZWFWFLMZNMZWFOMZPZQZUAABEEAVSP
    ZVRWEVFWRVLVTVQWDAVSVKUCWRAVSVPWCWRUDWRVOWBBOWRVMWAVNNAVSBLUEUFUFUGUHUIBWFP
    ZVFWGWEWQBWFEUJWSVTWKWDWPWSVKWJVSWSVJWIKWSVGWHKBWFUKULULSWSWCWOVSWSWBWNBWFO
    WSWAWLVNWMNBWFVSLUMWSVNWFBLMWMBWFBLUEBWFWFLUMUNUOWSUDUOUPUHUQVSWFAECURUSBEC
    URUSUTVAVBVC $.
    $( [29-Jun-2004] $) $( [5-Jun-2004] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x C $.
    $( The singletons of collinear vectors have the same span. $)
    spansncol $p |- ( ( A e. H~ /\ B e. CC /\ B =/= 0 ) ->
                    ( span ` { ( B .h A ) } ) = ( span ` { A } ) ) $=
      ( vx vy vz chil wcel cc cc0 wne w3a csm co csn cspn cfv cv wceq wrex wi
      wa cmul ax-hvmulass 3com13 3expa eqeq2d biimprd axmulcl ancoms adantll
      jctild opreq1 rcla4ev syl6 r19.23adva 3adant3 cdiv divclt 3expb adantlr
      simprl simplr syl3anc divcan1t 3exp com12 imp32 opreq1d eqtr3d exp43
      com4l 3imp r19.23adv impbid wb hvmulclt elspansnt syl 3ad2ant1 3bitr4d
      eqrdv ) AFGZBHGZBIJZKZCBALMZNOPZANOPZWECQZDQZWFLMZRZDHSZWIEQZALMZRZEHSZWI
      WGGZWIWHGZWEWMWQWBWCWMWQTWDWBWCUAZWLWQDHWTWJHGZUAZWLWJBUBMZHGZWIXCALMZRZU
      AWQXBWLXFXDXBXFWLXBXEWKWIWBWCXAXEWKRZXAWCWBXGWJBAUCUDUEUFUGWCXAXDWBXAWCXD
      WJBUHUIUJUKWPXFEXCHWNXCRWOXEWIWNXCALULUFUMUNUOUPWEWPWMEHWBWCWDWNHGZWPWMTZ
      TXHWBWCWDXIXHWBWCWDXIXHWBUAZWCWDUAZUAZWPWNBUQMZHGZWIXMWFLMZRZUAWMXLWPXPXN
      XLXPWPXLXOWOWIXLXMBUBMZALMZXOWOXNWCWBXRXORXLXMBAUCXHXKXNWBXHWCWDXNWNBURUS
      UTZXJWCWDVAXHWBXKVBVCXLXQWNALXHXKXQWNRZWBXHWCWDXTWCXHWDXTTWCXHWDXTBWNVDVE
      VFVGUTVHVIUFUGXSUKWLXPDXMHWJXMRWKXOWIWJXMWFLULUFUMUNVJVKVLVMVNWBWCWRWMVOZ
      WDWTWFFGZYAWCWBYBBAVPUIDWFWIVQVRUPWBWCWSWQVOWDEAWIVQVSVTWA $.
      $( [4-Jul-2004] $) $( [6-Jun-2004] $)
  $}

  $( Membership relation implied by equality of spans. $)
  spansneleqi $p |- ( A e. H~ ->
        ( ( span ` { A } ) = ( span ` { B } ) -> A e. ( span ` { B } ) ) ) $=
    ( csn cspn cfv wceq wcel chil eleq2 spansnid syl5cbi ) ACDEZBCDEZFALGAMGAHG
    LMAIAJK $.
    $( [9-Sep-2004] $) $( [6-Jun-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Membership relation that implies equality of spans. $)
    spansneleq $p |- ( ( B e. H~ /\ A =/= 0h ) ->
         ( A e. ( span ` { B } ) -> ( span ` { A } ) = ( span ` { B } ) ) ) $=
      ( vx chil wcel c0v wne wa csn cspn cfv cv csm co wceq cc wrex wb
      elspansnt adantr sneq fveq2d ad2antll wi cc0 opreq1 ax-hvmul0 sylan9eqr
      ex eqeq1 biimprd sylan9 necon3d com23 imp3a spansncol 3expia syld exp4b
      imp43 eqtrd exp32 r19.23adv sylbid ) BDEZAFGZHZABIJKZEZACLZBMNZOZCPQZAIZJ
      KZVHOZVEVIVMRVFCBASTVGVLVPCPVGVJPEZVLVPVGVQVLHHVOVKIZJKZVHVLVOVSOVGVQVLVN
      VRJAVKUAUBUCVEVFVQVLVSVHOZVEVQVFVLVTUDVEVQVFVLVTVEVQHVFVLHZVJUEGZVTVEWAWB
      UDVQVEVFVLWBVEVLVFWBVEVLVFWBUDVEVLHVJUEAFVEVJUEOZVKFOZVLAFOZVEWCWDWCVEVKU
      EBMNFVJUEBMUFBUGUHUIVLWEWDAVKFUJUKULUMUIUNUOTVEVQWBVTBVJUPUQURUSUNUTVAVBV
      CVD $.
      $( [24-Jun-2006] $) $( [6-Jun-2004] $)

    $( Membership relation that implies equality of spans. $)
    spansneleqOLD $p |- ( ( B e. H~ /\ -. A = 0h ) ->
         ( A e. ( span ` { B } ) -> ( span ` { A } ) = ( span ` { B } ) ) ) $=
      ( vx chil wcel c0v wceq wn wa csn cspn cfv cv csm co cc wrex wb
      elspansnt adantr sneq fveq2d ad2antll wi cc0 wne opreq1 ax-hvmul0
      sylan9eqr ex eqeq1 biimprd sylan9 con3d com23 imp3a df-ne syl6ibr
      spansncol 3expia syld exp4b imp43 eqtrd exp32 r19.23adv sylbid ) BDEZAFGZ
      HZIZABJKLZEZACMZBNOZGZCPQZAJZKLZVLGZVHVMVQRVJCBASTVKVPVTCPVKVNPEZVPVTVKWA
      VPIIVSVOJZKLZVLVPVSWCGVKWAVPVRWBKAVOUAUBUCVHVJWAVPWCVLGZVHWAVJVPWDUDVHWAV
      JVPWDVHWAIVJVPIZVNUEUFZWDVHWEWFUDWAVHWEVNUEGZHZWFVHVJVPWHVHVPVJWHVHVPVJWH
      UDVHVPIWGVIVHWGVOFGZVPVIVHWGWIWGVHVOUEBNOFVNUEBNUGBUHUIUJVPVIWIAVOFUKULUM
      UNUJUOUPVNUEUQURTVHWAWFWDBVNUSUTVAVBUOVCVDVEVFVG $.
      $( [4-Jul-2004] $) $( [6-Jun-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The span of the singleton of an element of a subspace is included in the
       subspace. $)
    spansnsst $p |- ( ( A e. SH /\ B e. A ) -> ( span ` { B } ) (_ A ) $=
      ( vx vy csh wcel wa csn cspn cfv cv csm co wceq cc wrex chil wb shelt
      elspansnt syl wi shmulcltOLD eleq1 biimprd syl9 com23 exp3a imp
      r19.23adv sylbid ssrdv ) AEFZBAFZGZCBHIJZAUOCKZUPFZUQDKZBLMZNZDOPZUQAFZUO
      BQFURVBRBASDBUQTUAUOVAVCDOUMUNUSOFZVAVCUBZUBUMVDUNVEUMVDUNVEUMVAVDUNGZVCU
      MVFUTAFZVAVCUSBAUCVAVCVGUQUTAUDUEUFUGUHUGUIUJUKUL $.
      $( [4-Jul-2004] $) $( [5-Jun-2004] $)
  $}

  $( A member of the span of the singleton of a vector is a member of a
     subspace containing the vector. $)
  elspansn3t $p |- ( ( A e. SH /\ B e. A /\ C e. ( span ` { B } ) ) ->
                   C e. A ) $=
    ( csh wcel csn cspn cfv wa spansnsst sseld 3impia ) ADEZBAEZCBFGHZECAEMNIOA
    CABJKL $.
    $( [19-Jun-2006] $) $( [16-Dec-2004] $)

  $( A span membership condition implying two vectors belong to the same
     subspace. $)
  elspansn4t $p |- ( ( ( A e. SH /\ B e. H~ ) /\
         ( C e. ( span ` { B } ) /\ C =/= 0h ) ) -> ( B e. A <-> C e. A ) ) $=
    ( csh wcel chil wa csn cspn cfv c0v wne wi elspansn3t 3exp com23 imp
    ad2ant2r 3expia spansnid ad2antrr wceq spansneleq eleqtrrd syl5 exp4b
    com24 exp4a imp43 impbid ) ADEZBFEZGCBHIJZEZCKLZGGBAEZCAEZUKUNUPUQMZULUOUKU
    NURUKUPUNUQUKUPUNUQABCNOPQRUKULUNUOUQUPMZUKUNULUOUSMUKUNULUOUSUKUQULUOGZUNU
    PUKUQUTUNUPUKUQGBCHIJZEZUPUTUNGZUKUQVBUPACBNSVCBUMVAULBUMEUOUNBTUAUTUNVAUMU
    BCBUCQUDUEUFUGUHPUIUJ $.
    $( [27-Dec-2007] $) $( [17-Dec-2004] $)

  $( A vector belonging to both a subspace and the span of the singleton of a
     vector not in it must be zero. $)
  elspansn5t $p |- ( A e. SH -> ( ( ( B e. H~ /\ -. B e. A ) /\
         ( C e. ( span ` { B } ) /\ C e. A ) ) -> C = 0h ) ) $=
    ( csh wcel chil wn csn cspn cfv wa c0v wceq wi wne elspansn4t biimprd
    exp32 com34 imp32 necon1bd exp31 imp4c ) ADEZBFEZBAEZGZCBHIJEZCAEZKZCLMZUDU
    EUJUGUKUDUEUJUGUKNUDUEKZUJKUFCLULUHUICLOZUFNULUHUMUIUFULUHUMUIUFNULUHUMKKUF
    UIABCPQRSTUAUBSUC $.
    $( [22-Dec-2004] $) $( [17-Dec-2004] $)

  $( The span of the singleton of an element of a subspace is included in the
     subspace. $)
  spansnss2t $p |- ( ( A e. SH /\ B e. H~ ) ->
                   ( B e. A <-> ( span ` { B } ) (_ A ) ) $=
    ( csh wcel chil wa csn cspn cfv wss wi spansnsst ex adantr ssel spansnid
    syl5com adantl impbid ) ACDZBEDZFBADZBGHIZAJZTUBUDKUATUBUDABLMNUAUDUBKTUDBU
    CDUBUAUCABOBPQRS $.
    $( [5-Jan-2005] $) $( [16-Dec-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Cancellation-type law that "extracts" a vector ` A ` from its inner
       product with a proportional vector ` B ` . $)
    normcant $p |- ( ( B e. H~ /\ B =/= 0h /\ A e. ( span ` { B } ) ) ->
            ( ( ( A .ih B ) / ( ( normh ` B ) ^ 2 ) ) .h B ) = A ) $=
      ( vx chil wcel c0v wne csn cspn cfv csp co cno c2 cexp cdiv csm wceq wa
      cv cc wrex wb elspansnt adantr cmul opreq1 ax-his3 pm3.27 pm3.26 syl3anc
      sylan9eqr normsqt ad2antrr opreq12d adantllr cc0 divcan4t hiclt anidms
      his6t necon3bid biimpar eqtrd opreq1d eqtr4d exp31 r19.23adv sylbid
      3impia ) BDEZBFGZABHIJEZABKLZBMJNOLZPLZBQLZARZVKVLSZVMACTZBQLZRZCUAUBZVRV
      KVMWCUCVLCBAUDUEVSWBVRCUAVSVTUAEZWBVRVSWDSZWBSZVQWAAWFVPVTBQWFVPVTBBKLZUF
      LZWGPLZVTVKWDWBVPWIRVLVKWDSZWBSVNWHVOWGPWBWJVNWABKLZWHAWABKUGWDVKVKWKWHRW
      JVTBBUHVKWDUIVKWDUJZWLUKULVKVOWGRWDWBBUMUNUOUPWEWIVTRZWBWGUAEZWDWGUQGZWMW
      EWGVTURVKWNVLWDVKWNBBUSUTUNVSWDUIVSWOWDVKWOVLVKWGUQBFBVAVBVCUEUKUEVDVEWEW
      BUIVFVGVHVIVJ $.
      $( [28-May-2006] $) $( [18-Mar-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A projection on the span of a singleton. $)
    pjspansnt $p |- ( ( A e. H~ /\ B e. H~ /\ A =/= 0h ) -> ( ( proj ` ( span `
        { A } ) ) ` B ) = ( ( ( B .ih A ) / ( ( normh ` A ) ^ 2 ) ) .h A ) ) $=
      ( vx vy chil wcel c0v wne w3a csn cspn cfv cpj cv cva co wceq cort wrex
      crab cuni csp cno c2 cexp cdiv csm cch pjvalt spansncht sylan 3adant3
      wss wi wa opreq1 ad2antll caddc cc0 chelt chocclt syl adantlr ax-his2
      3comr 3expa syldan csh shocorth 3impib spansnsht adantr spansnid pm3.27
      syl3anc wb orthcom mpbid opreq2d cc hiclt ancoms ax0id 3eqtrd pm3.26
      elspansnclt jca adantrr eqtrd adantllr opreq1d normcant eqtr2d exp32
      r19.23adv 3adantl2 ss2rabdv wreu pjthut reuunisn spansnmul 3simp1 divclt
      normclt recnd sqclt 3ad2ant1 sqne0t normne0t bitr2d biimpa 3adant2
      sylanc rabsn 3sstr3d fvex rabex uniex snsssn ) AEFZBEFZAGHZIZBAJZKLZMLLZB
      CNZDNZOPZQZDYERLZSZCYETZUAZBAUBPZAUCLZUDUEPZUFPZAUGPZXTYAYFYNQZYBYEUHFZYA
      YTXTCDBYEUIAUJZUKULYCYNJZYSJZUMYNYSQYCYMYGYSQZCYETZUUCUUDYCYLUUECYEXTYBYG
      YEFZYLUUEUNYAXTYBUOUUGUOZYJUUEDYKUUHYHYKFZYJUUEUUHUUIYJUOZUOZYSYGAUBPZYQU
      FPZAUGPZYGUUKYRUUMAUGUUKYOUULYQUFXTUUGUUJYOUULQYBXTUUGUOZUUJUOYOYIAUBPZUU
      LYJYOUUPQUUOUUIBYIAUBUPUQUUOUUIUUPUULQZYJXTYGEFZUOZUUIUUQUUOUUSUUIUOZUUPU
      ULYHAUBPZURPZUULUSURPZUULUUSUUIYHEFZUUPUVBQZXTUUIUVDUURYKUHFZUUIUVDXTYHYK
      UTXTUUAUVFUUBYEVAVBUKZVCXTUURUVDUVEUURUVDXTUVEYGYHAVDVEVFVGUUTUVAUSUULURX
      TUUIUVAUSQZUURXTUUIUOZAYHUBPUSQZUVHYEVHFZAYEFZUUIUVJUVIUVKUVLUUIUVJAYHYEV
      IVJXTUVKUUIAVKVLXTUVLUUIAVMVLXTUUIVNVOXTUUIUVDUVJUVHVPUVGAYHVQVGVRVCVSUUS
      UVCUULQZUUIUUSUULVTFZUVMUURXTUVNYGAWAWBUULWCVBVLWDUUOXTUURXTUUGWEAYGWFWGU
      KWHWIWJWKWKUUHUUNYGQZUUJXTYBUUGUVOYGAWLVFVLWMWNWOWPWQYCYLCYEWRZYMUUCQXTYA
      UVPYBUUAYAUVPXTCDBYEWSUUBUKULYLCYEWTVBYCYSYEFZUUFUUDQXTYRVTFZUVQYCAYRXAXT
      YAYBXBYOVTFZYQVTFZYQUSHZUVRYCYOYQXCXTYAUVSYBYAXTUVSBAWAWBULXTYAUVTYBXTYPV
      TFZUVTXTYPAXDXEZYPXFVBXGXTYBUWAYAXTYBUWAXTUWAYPUSHZYBXTUWBUWAUWDVPUWCYPXH
      VBAXIXJXKXLVOXMCYEYSXNVBXOYNYSYMYLCYEYDKXPXQXRXSVBWI $.
      $( [12-Jul-2006] $) $( [28-May-2006] $)
  $}

  ${
    spansnpj.1 $e |- A (_ H~ $.
    spansnpj.2 $e |- B e. H~ $.
    $( A subset of Hilbert space is orthogonal to the span of the singleton of
       a projection onto its orthocomplement. $)
    spansnpj $p |- A (_
                 ( _|_ ` ( span ` { ( ( proj ` ( _|_ ` A ) ) ` B ) } ) ) $=
      ( cort cfv cpj csn cspn chil wss ococss ax-mp occl chssi wcel pjcli
      snssi spanss mp2an csh wceq chshi spanid sseqtr pjhcli spansnch chsscon3
      mpbi sstri ) AAEFZEFZBUKGFFZHZIFZEFZAJKAULKCALMUOUKKULUPKUOUKIFZUKUKJKUNU
      KKZUOUQKUKACNZOUMUKPURBUKUSDQUMUKRMUNUKSTUKUAPUQUKUBUKUSUCUKUDMUEUOUKUMBU
      KUSDUFUGUSUHUIUJ $.
      $( [4-Jul-2004] $) $( [4-Jun-2004] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.
    spanunsn.1 $e |- A e. CH $.
    spanunsn.2 $e |- B e. H~ $.
    $( The span of the union of a closed subspace with a singleton equals
       the span of its union with an orthogonal singleton. $)
    spanunsn $p |- ( span ` ( A u. { B } ) ) =
             ( span ` ( A u. { ( ( proj ` ( _|_ ` A ) ) ` B ) } ) ) $=
      ( vx vy vz vw vv vu csn cspn cfv cph co cort cpj cun cv wcel cva wceq
      wrex chshi chil wss csh snssi ax-mp spanclt shsel csm cc wi wa eleq1
      biimparc rcla4eopr shaddcltOLD pjcli shmulcltOLD mpan2 sylan2i choccl
      pjhcli spansnmul mpan adantl ax-hvdistr1 mp3an23 pjpj opreq2i syl5eq
      opreq2d ax-hvass 3expb chel hvmulclt jca syl2an eqtr4d syl3anc sylibr
      opreq2 eqeq2d biimpa exp43 r19.23adv elspansn syl5ib r19.23aiv sylbi
      cneg negclt syl sylani ancoms c0v c1 hvm1negt hvnegidt ax-hvcom sylanc
      3eqtr3d opreq1d hvaddclt hvaddid2t hvadd4t anim12i impbi eqriv chssi
      spanun spanid opreq1i eqtr 3eqtr4 ) ABKZLMZNOZABAPMZQMMZKZLMZNOZAXRRLMZAY
      CRLMZEXTYEESZXTTZYHYETZYIYHFSZGSZUAOZUBZGXSUCZFAUCYJFGAXSYHACUDZXRUEUFZXS
      UGTBUETZYQDBUEUHUIZXRUJUIZUKYOYJFAYKATZYNYJGXSUUAYLHSZBULOZUBZHUMUCYNYJUN
      ZYLXSTUUAUUDUUEHUMUUAUUBUMTZUUDYNYJYKUUCUAOZYETZYHUUGUBZYJUUAUUFUOZUUDYNU
      OUUIYJUUHYHUUGYEUPUQUUJUUGISJSUAOZUBJYDUCIAUCZUUHYKUUBBAQMMZULOZUAOZATZUU
      BYBULOZYDTZUUGUUOUUQUAOZUBUULUUJIJAYDUUOUUQUUGUAURAUGTZUUJUUPUNYPUUTUUAUU
      NATZUUPUUFYKUUNAUSUUFUUMATZUVABACDUTZUUTUUFUVBUOUVAUNYPUUBUUMAVAUIVBVCUIU
      UFUURUUAYBUETZUUFUURBYAACVDDVEZYBUUBVFVGVHUUJUUGYKUUNUUQUAOZUAOZUUSUUJUUC
      UVFYKUAUUFUUCUVFUBUUAUUFUUBUUMYBUAOZULOZUVFUUCUUFUUMUETZUVDUVIUVFUBBACDVE
      ZUVEUUBUUMYBVIVJBUVHUUBULBACDVKVLVMVHZVNYKUETZUUNUETZUUQUETZUOUUSUVGUBZUU
      AUUFUVMUVNUVOUVPYKUUNUUQVOVPYKACVQZUUFUVNUVOUUFUVJUVNUVKUUBUUMVRVBZUUFUVD
      UVOUVEUUBYBVRVBZVSVTWAWBIJAYDUUGYPYCUEUFZYDUGTUVDUVTUVEYBUEUHUIZYCUJUIZUK
      WCUUDYNUUIUUDYMUUGYHYLUUCYKUAWDWEWFVTWGWHHBYLDWIWJWHWKWLYJYNGYDUCZFAUCYIF
      GAYDYHYPUWBUKUWCYIFAUUAYNYIGYDUUAYLUUQUBZHUMUCYNYIUNZYLYDTUUAUWDUWEHUMUUA
      UUFUWDYNYIYKUUQUAOZXTTZYHUWFUBZYIUUJUWDYNUOUWHYIUWGYHUWFXTUPUQUUJUWFUUKUB
      JXSUCIAUCZUWGUUBWMZUUMULOZYKUAOZATZUUCXSTZUWFUWLUUCUAOZUBUWIUUJIJAXSUWLUU
      CUWFUAURUUFUUAUWMUUTUUFUUAUOUWMUNYPUUTUWKATZUUAUWMUUFUWKYKAUSUUFUWJUMTZUW
      PUUBWNZUWQUVBUWPUVCUUTUWQUVBUOUWPUNYPUWJUUMAVAUIVBWOWPUIWQUUFUWNUUAYRUUFU
      WNDBUUBVFVGVHUUJUWFUWLUVFUAOZUWOUUJWRUWFUAOZUWKUUNUAOZUWFUAOZUWFUWSUUJWRU
      XAUWFUAUUFWRUXAUBUUAUUFUUNWSWMUUNULOZUAOZUUNUWKUAOZWRUXAUUFUXCUWKUUNUAUUF
      UVJUXCUWKUBUVKUUBUUMWTVBVNUUFUVNUXDWRUBUVRUUNXAWOUVNUWKUETZUXEUXAUBUUFUUN
      UWKXBUVRUUFUWQUXFUWRUWQUVJUXFUVKUWJUUMVRVBWOZXCXDVHXEUUJUWFUETZUWTUWFUBUV
      MUVOUXHUUAUUFYKUUQXFUVQUVSVTUWFXGWOUXFUVNUOZUVMUVOUOUXBUWSUBUUJUWKUUNYKUU
      QXHUUFUXIUUAUUFUXFUVNUXGUVRVSVHUUAUVMUUFUVOUVQUVSXIXCXDUUJUUCUVFUWLUAUVLV
      NWAWBIJAXSUWFYPYTUKWCUWDYNUWHUWDYMUWFYHYLUUQYKUAWDWEWFVTWGWHHYBYLUVEWIWJW
      HWKWLXJXKYFALMZXSNOXTAXRACXLZYSXMUXJAXSNUUTUXJAUBYPAXNUIZXOXPYGUXJYDNOYEA
      YCUXKUWAXMUXJAYDNUXLXOXPXQ $.
      $( [6-Jul-2004] $) $( [3-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The span of a pair of vectors. $)
    spanpr $p |- ( ( A e. H~ /\ B e. H~ ) ->
                 ( span ` { ( A +h B ) } ) (_ ( span ` { A , B } ) ) $=
      ( vx chil wcel wa cva co csn cspn cfv cph cpr cv csh elspansn3t shsclt
      spansnsht syl2an adantr shsvat anim12i spansnid sylc pm3.27 syl3anc ex
      ssrdv cun wss wceq spanunt snssi df-pr fveq2i syl5req sseqtrd ) ADEZBDEZF
      ZABGHZIJKZAIZJKZBIZJKZLHZABMZJKZUTCVBVGUTCNZVBEZVJVGEZVGOEZVAVGEZVKVLUTVK
      FVGVAVJPUTVMVKVDOEZVFOEZVMURUSVDVFQARZBRZSTUTVNVKVOVPFAVDEZBVFEZFVNUTVDVF
      ABUAURVOUSVPVQVRUBURVSUSVTAUCBUCUBUDTUTVKUEUFUGUHUTVCVEUIZJKZVGVIVCDUJVED
      UJWBVGUKURUSVCVEULADUMBDUMSVHWAJABUNUOUPUQ $.
      $( [19-Jun-2006] $) $( [9-Jun-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    h1datom.1 $e |- A e. CH $.
    h1datom.2 $e |- B e. H~ $.
    $( A 1-dimensional subspace is an atom. $)
    h1datom $p |- ( A (_ ( _|_ ` ( _|_ ` { B } ) ) ->
                  ( A = ( _|_ ` ( _|_ ` { B } ) ) \/ A = 0H ) ) $=
      ( vx vy csn cort cfv wss wne c0h wceq wi wo wn wa wcel cv c0v wrex ssel
      csm co cc cc0 eqeq1 opreq1 chil ax-hvmul0 ax-mp syl6eq syl5bir necon3d
      adantl c1 cdiv recclt csh chshi shmulclt mp3an1 ex syl adantr opreq2
      cmul ax-hvmulass mp3an3 pm3.26 sylanc recid2t opreq1d eqtr3d ax-hvmulid
      sylan9eqr eleq1d sylibd exp31 com23 imp syld com3r exp3a r19.23adv
      h1de2ct syl5ib sylcom chne0 snssi chssi occon2 ococ syl6ss syl6 anc2li
      eqss syl6ibr df-ne syl5ibr necon1ad neor sylibr ) ABGZHIHIZJZAXEKALMZNAXE
      MZXGOXFXGAXEXFALKZXHXGPXFXIXFXEAJZQXHXFXIXJXFXIBARZXJXFESZTKZEAUAXKXIXFXM
      XKEAXFXLARZXLXERZXMXKNZAXEXLUBXNXLFSZBUCUDZMZFUEUAXPXOXNXSXPFUEXNXQUERZXS
      XPXTXSQZXMXNXKYAXMXQUFKZXNXKNZXSXMYBNXTXSXQUFXLTXSXLTMXRTMXQUFMZXLXRTUGYD
      XRUFBUCUDZTXQUFBUCUHBUIRZYETMDBUJUKULUMUNUOXTXSYBYCNXTYBXSYCXTYBXSYCXTYBQ
      ZXSQZXNUPXQUQUDZXLUCUDZARZXKYGXNYKNZXSYGYIUERZYLXQURZYMXNYKAUSRYMXNYKACUT
      YIXLAVAVBVCVDVEYHYJBAXSYGYJYIXRUCUDZBXLXRYIUCVFYGYOUPBUCUDZBYGYIXQVGUDZBU
      CUDZYOYPYMXTYRYOMZYGYMXTYFYSDYIXQBVHVIYNXTYBVJVKYGYQUPBUCXQVLVMVNYFYPBMDB
      VOUKULVPVQVRVSVTWAWBWCWDWEFXLBDWFWGWHWEEACWIWGXKXEAHIHIZAXKXDAJXEYTJBAWJX
      DAYFXDUIJDBUIWJUKACWKWLVDACWMWNWOWPAXEWQWRALWSWTXAXGAXEXBXC $.
      $( [20-Jul-2001] $)
  $}

  $( A 1-dimensional subspace is an atom. $)
  h1datomt $p |- ( ( A e. CH /\ B e. H~ ) ->
                ( A (_ ( _|_ ` ( _|_ ` { B } ) ) ->
                ( A = ( _|_ ` ( _|_ ` { B } ) ) \/ A = 0H ) ) ) $=
    ( cch wcel chil csn cort cfv wss wceq c0h wo wi cif c0v sseq1 eqeq1
    orbi12d imbi12d sneq fveq2d sseq2d eqeq2d orbi1d h0elch elimel ax-hv0cl
    h1datom dedth2h ) ACDZBEDZABFZGHZGHZIZAUNJZAKJZLZMUJAKNZUNIZUSUNJZUSKJZLZMU
    SUKBONZFZGHZGHZIZUSVGJZVBLZMABKOAUSJZUOUTURVCAUSUNPVKUPVAUQVBAUSUNQAUSKQRSB
    VDJZUTVHVCVJVLUNVGUSVLUMVFGVLULVEGBVDTUAUAZUBVLVAVIVBVLUNVGUSVMUCUDSUSVDAKC
    UEUFBOEUGUFUHUI $.
    $( [22-Jul-2001] $)

