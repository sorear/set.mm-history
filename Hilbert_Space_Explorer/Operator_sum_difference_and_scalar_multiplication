$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Operator sum, difference, and scalar multiplication
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g h x y $.
    $( Define the sum of two Hilbert space operators.  Definition of [Beran]
       p. 111.

       _Note on operators_.  Unlike some authors, we use the term "operator" to
       mean any function from ` ~H ` to ` ~H ` .  This is the definition of
       operator in [Hughes] p. 14, the definition of operator in
       [AkhiezerGlazman] p. 30, and the definition of operator in [Goldberg]
       p. 10.  For Reed and Simon, an operator is linear (definition of
       operator in [ReedSimon] p. 2).  For Halmos, an operator is bounded and
       linear (definition of operator in [Halmos] p. 35).  For Kalmbach and
       Beran, an operator is continuous and linear (definition of operator in
       [Kalmbach] p. 353; definition of operator in [Beran] p. 99).  Note that
       "bounded and linear" and "continuous and linear" are equivalent by
       ~ lncnbd . $)
    df-hosum $a |- +op = ( f e. ( ~H ^m ~H ) , g e. ( ~H ^m ~H ) |->
          ( x e. ~H |-> ( ( f ` x ) +h ( g ` x ) ) ) ) $.

    $( Define the scalar product with a Hilbert space operator.  Definition of
       [Beran] p. 111. $)
    df-homul $a |- .op = ( f e. CC , g e. ( ~H ^m ~H ) |->
          ( x e. ~H |-> ( f .h ( g ` x ) ) ) ) $.

$(
    @( Define the difference of two Hilbert space operators.  Definition of
       [Beran] p. 111. @)
    df-hodif @a |- -op = { <. <. f , g >. , h >. | ( ( f : ~H --> ~H /\
          g : ~H --> ~H ) /\ h = ( f +op ( -u 1 .op g ) ) ) } @.
$)

    $( Define the difference of two Hilbert space operators.  Definition of
       [Beran] p. 111. $)
    df-hodif $a |- -op = ( f e. ( ~H ^m ~H ) , g e. ( ~H ^m ~H ) |->
          ( x e. ~H |-> ( ( f ` x ) -h ( g ` x ) ) ) ) $.

    $( Define the sum of two Hilbert space functionals.  Definition of [Beran]
       p. 111.  Note that unlike some authors, we define a functional as any
       function from ` ~H ` to ` CC ` , not just linear (or bounded linear)
       ones. $)
    df-hfsum $a |- +fn = ( f e. ( CC ^m ~H ) , g e. ( CC ^m ~H ) |->
          ( x e. ~H |-> ( ( f ` x ) + ( g ` x ) ) ) ) $.

    $( Define the scalar product with a Hilbert space functional.  Definition
       of [Beran] p. 111. $)
    df-hfmul $a |- .fn = ( f e. CC , g e. ( CC ^m ~H ) |->
          ( x e. ~H |-> ( f x. ( g ` x ) ) ) ) $.
  $}

  ${
    $d f g h x y A $.  $d x y f g h S $.  $d x y f g h T $.
    $( Value of the sum of two Hilbert space operators. $)
    hosmval $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) -> ( S +op T ) =
                    ( x e. ~H |-> ( ( S ` x ) +h ( T ` x ) ) ) ) $=
      ( vf vg chil wf co wcel chos cv cfv cmpt wceq ax-hilex elmap fveq1 adantr
      cva cvv oveq1d mpteq2dva oveq2d df-hosum mptexg ax-mp ovmpt2 syl2anbr
      cmap ) FFBGBFFUIHZICUJIBCJHAFAKZBLZUKCLZSHZMZNFFCGFFBOOPFFCOOPDEBCUJUJAFU
      KDKZLZUKEKZLZSHZMUOJAFULUSSHZMUPBNZAFUTVAVBUTVANUKFIZVBUQULUSSUKUPBQUARUB
      URCNZAFVAUNVDVAUNNVCVDUSUMULSUKURCQUCRUBADEUDFTIUOTIOAFUNTUEUFUGUH $.
      $( [16-Nov-2013] $) $( [9-Nov-2000] $)

    $( Value of the scalar product with a Hilbert space operator. $)
    hommval $p |- ( ( A e. CC /\ T : ~H --> ~H ) -> ( A .op T ) =
                    ( x e. ~H |-> ( A .h ( T ` x ) ) ) ) $=
      ( vf vg chil wf cc wcel co chot cv cfv csm cmpt ax-hilex adantr mpteq2dva
      wceq cvv elmap oveq1 fveq1 oveq2d df-homul mptexg ax-mp ovmpt2 sylan2br
      cmap ) FFCGBHICFFUJJZIBCKJAFBALZCMZNJZOZSFFCPPUADEBCHUKAFDLZULELZMZNJZOUO
      KAFBURNJZOUPBSZAFUSUTVAUSUTSULFIZUPBURNUBQRUQCSZAFUTUNVCUTUNSVBVCURUMBNUL
      UQCUCUDQRADEUEFTIUOTIPAFUNTUFUGUHUI $.
      $( [16-Nov-2013] $) $( [20-Feb-2006] $)

    $( Value of the difference of two Hilbert space operators. $)
    hodmval $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) -> ( S -op T ) =
                    ( x e. ~H |-> ( ( S ` x ) -h ( T ` x ) ) ) ) $=
      ( vf vg chil wf co wcel chod cv cfv cmpt wceq ax-hilex elmap fveq1 adantr
      cmv cvv oveq1d mpteq2dva oveq2d df-hodif mptexg ax-mp ovmpt2 syl2anbr
      cmap ) FFBGBFFUIHZICUJIBCJHAFAKZBLZUKCLZSHZMZNFFCGFFBOOPFFCOOPDEBCUJUJAFU
      KDKZLZUKEKZLZSHZMUOJAFULUSSHZMUPBNZAFUTVAVBUTVANUKFIZVBUQULUSSUKUPBQUARUB
      URCNZAFVAUNVDVAUNNVCVDUSUMULSUKURCQUCRUBADEUDFTIUOTIOAFUNTUEUFUGUH $.
      $( [16-Nov-2013] $) $( [9-Nov-2000] $)

    $( Value of the sum of two Hilbert space functionals. $)
    hfsmval $p |- ( ( S : ~H --> CC /\ T : ~H --> CC ) -> ( S +fn T ) =
                    ( x e. ~H |-> ( ( S ` x ) + ( T ` x ) ) ) ) $=
      ( vf vg chil cc wf co wcel chfs cv cfv caddc cmpt wceq ax-hilex elmap cvv
      cnex fveq1 oveq1d adantr mpteq2dva oveq2d df-hfsum mptexg ovmpt2 syl2anbr
      cmap ax-mp ) FGBHBGFUJIZJCULJBCKIAFALZBMZUMCMZNIZOZPFGCHGFBTQRGFCTQRDEBCU
      LULAFUMDLZMZUMELZMZNIZOUQKAFUNVANIZOURBPZAFVBVCVDVBVCPUMFJZVDUSUNVANUMURB
      UAUBUCUDUTCPZAFVCUPVFVCUPPVEVFVAUOUNNUMUTCUAUEUCUDADEUFFSJUQSJQAFUPSUGUKU
      HUI $.
      $( [16-Nov-2013] $) $( [23-May-2006] $)

    $( Value of the scalar product with a Hilbert space functional. $)
    hfmmval $p |- ( ( A e. CC /\ T : ~H --> CC ) -> ( A .fn T ) =
                    ( x e. ~H |-> ( A x. ( T ` x ) ) ) ) $=
      ( vf vg chil cc wf wcel chft cfv cmul cmpt wceq ax-hilex adantr mpteq2dva
      co cv cvv cmap cnex elmap oveq1 fveq1 oveq2d df-hfmul mptexg ax-mp ovmpt2
      sylan2br ) FGCHBGICGFUARZIBCJRAFBASZCKZLRZMZNGFCUBOUCDEBCGULAFDSZUMESZKZL
      RZMUPJAFBUSLRZMUQBNZAFUTVAVBUTVANUMFIZUQBUSLUDPQURCNZAFVAUOVDVAUONVCVDUSU
      NBLUMURCUEUFPQADEUGFTIUPTIOAFUOTUHUIUJUK $.
      $( [16-Nov-2013] $) $( [23-May-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y S $.  $d x y T $.
    $( Value of the sum of two Hilbert space operators. $)
    hosval $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ A e. ~H ) ->
        ( ( S +op T ) ` A ) = ( ( S ` A ) +h ( T ` A ) ) ) $=
      ( vx chil wf wcel chos co cfv cva wceq wa cv hosmval fveq1d fveq2 oveq12d
      cmpt eqid ovex fvmpt sylan9eq 3impa ) EEBFZEECFZAEGZABCHIZJZABJZACJZKIZLU
      EUFMZUGUIADEDNZBJZUNCJZKIZSZJULUMAUHURDBCOPDAUQULEURUNALUOUJUPUKKUNABQUNA
      CQRURTUJUKKUAUBUCUD $.
      $( [16-Nov-2013] $) $( [10-Nov-2000] $)

    $( Value of the scalar product with a Hilbert space operator. $)
    homval $p |- ( ( A e. CC /\ T : ~H --> ~H /\ B e. ~H ) ->
        ( ( A .op T ) ` B ) = ( A .h ( T ` B ) ) ) $=
      ( vx cc wcel chil wf chot co cfv csm wceq wa cv cmpt hommval fveq1d fveq2
      oveq2d eqid ovex fvmpt sylan9eq 3impa ) AEFZGGCHZBGFZBACIJZKZABCKZLJZMUFU
      GNZUHUJBDGADOZCKZLJZPZKULUMBUIUQDACQRDBUPULGUQUNBMUOUKALUNBCSTUQUAAUKLUBU
      CUDUE $.
      $( [16-Nov-2013] $) $( [20-Feb-2006] $)

    $( Value of the difference of two Hilbert space operators. $)
    hodval $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ A e. ~H ) ->
        ( ( S -op T ) ` A ) = ( ( S ` A ) -h ( T ` A ) ) ) $=
      ( vx chil wf wcel chod co cfv cmv wceq wa cv hodmval fveq1d fveq2 oveq12d
      cmpt eqid ovex fvmpt sylan9eq 3impa ) EEBFZEECFZAEGZABCHIZJZABJZACJZKIZLU
      EUFMZUGUIADEDNZBJZUNCJZKIZSZJULUMAUHURDBCOPDAUQULEURUNALUOUJUPUKKUNABQUNA
      CQRURTUJUKKUAUBUCUD $.
      $( [16-Nov-2013] $) $( [10-Nov-2000] $)

    $( Value of the sum of two Hilbert space functionals. $)
    hfsval $p |- ( ( S : ~H --> CC /\ T : ~H --> CC /\ A e. ~H ) ->
        ( ( S +fn T ) ` A ) = ( ( S ` A ) + ( T ` A ) ) ) $=
      ( vx chil cc wf wcel chfs co caddc wceq wa cv cmpt hfsmval fveq1d oveq12d
      cfv fveq2 eqid ovex fvmpt sylan9eq 3impa ) EFBGZEFCGZAEHZABCIJZSZABSZACSZ
      KJZLUFUGMZUHUJADEDNZBSZUOCSZKJZOZSUMUNAUIUSDBCPQDAURUMEUSUOALUPUKUQULKUOA
      BTUOACTRUSUAUKULKUBUCUDUE $.
      $( [16-Nov-2013] $) $( [23-May-2006] $)

    $( Value of the scalar product with a Hilbert space functional. $)
    hfmval $p |- ( ( A e. CC /\ T : ~H --> CC /\ B e. ~H ) ->
        ( ( A .fn T ) ` B ) = ( A x. ( T ` B ) ) ) $=
      ( vx cc wcel chil wf chft co cfv cmul wceq wa cv cmpt fveq1d fveq2 oveq2d
      hfmmval eqid ovex fvmpt sylan9eq 3impa ) AEFZGECHZBGFZBACIJZKZABCKZLJZMUF
      UGNZUHUJBDGADOZCKZLJZPZKULUMBUIUQDACTQDBUPULGUQUNBMUOUKALUNBCRSUQUAAUKLUB
      UCUDUE $.
      $( [16-Nov-2013] $) $( [23-May-2006] $)
  $}

  $( Closure of the sum of two Hilbert space operators. $)
  hoscl $p |- ( ( ( S : ~H --> ~H /\ T : ~H --> ~H ) /\ A e. ~H ) ->
      ( ( S +op T ) ` A ) e. ~H ) $=
    ( chil wf wa wcel chos co cfv cva wceq hosval 3expa ffvelrn anim12i hvaddcl
    anandirs syl eqeltrd ) DDBEZDDCEZFADGZFZABCHIJZABJZACJZKIZDUAUBUCUEUHLABCMN
    UDUFDGZUGDGZFZUHDGUAUBUCUKUAUCFUIUBUCFUJDDABODDACOPRUFUGQST $.
    $( [12-Nov-2000] $)

  $( Closure of the scalar product of a Hilbert space operator. $)
  homcl $p |- ( ( A e. CC /\ T : ~H --> ~H /\ B e. ~H ) ->
      ( ( A .op T ) ` B ) e. ~H ) $=
    ( cc wcel chil wf w3a co cfv csm homval wa ffvelrn anim2i 3impb hvmulcl syl
    chot eqeltrd ) ADEZFFCGZBFEZHZBACSIJABCJZKIZFABCLUDUAUEFEZMZUFFEUAUBUCUHUBU
    CMUGUAFFBCNOPAUEQRT $.
    $( [12-Oct-2006] $) $( [20-Feb-2006] $)

  $( Closure of the difference of two Hilbert space operators. $)
  hodcl $p |- ( ( ( S : ~H --> ~H /\ T : ~H --> ~H ) /\ A e. ~H ) ->
      ( ( S -op T ) ` A ) e. ~H ) $=
    ( chil wcel chod cfv w3a cmv hodval ffvelrn 3adant2 3adant1 hvsubcl syl2anc
    wf co eqeltrd 3expa ) DDBPZDDCPZADEZABCFQGZDETUAUBHZUCABGZACGZIQZDABCJUDUED
    EZUFDEZUGDETUBUHUADDABKLUAUBUITDDACKMUEUFNORS $.
    $( [15-Nov-2002] $)

