$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Operator sum, difference, and scalar multiplication
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g h x y $.
    $( Define the sum of two Hilbert space operators.  Definition of [Beran]
       p. 111.

       _Note on operators_.  Unlike some authors, we use the term "operator" to
       mean any function from ` ~H ` to ` ~H ` .  This is the definition of
       operator in [Hughes] p. 14, the definition of operator in
       [AkhiezerGlazman] p. 30, and the definition of operator in [Goldberg]
       p. 10.  For Reed and Simon, an operator is linear (definition of
       operator in [ReedSimon] p. 2).  For Halmos, an operator is bounded and
       linear (definition of operator in [Halmos] p. 35).  For Kalmbach and
       Beran, an operator is continuous and linear (definition of operator in
       [Kalmbach] p. 353; definition of operator in [Beran] p. 99).  Note that
       "bounded and linear" and "continuous and linear" are equivalent by
       ~ lncnbd . $)
    df-hosum $a |- +op = { <. <. f , g >. , h >. | ( ( f : ~H --> ~H /\
          g : ~H --> ~H ) /\
          h = { <. x , y >. | ( x e. ~H /\
           y = ( ( f ` x ) +h ( g ` x ) ) ) } ) } $.

    $( Define the scalar product with a Hilbert space operator.  Definition of
       [Beran] p. 111. $)
    df-homul $a |- .op = { <. <. f , g >. , h >. | ( ( f e. CC /\
          g : ~H --> ~H ) /\
          h = { <. x , y >. | ( x e. ~H /\
           y = ( f .h ( g ` x ) ) ) } ) } $.

$(
    @( Define the difference of two Hilbert space operators.  Definition of
       [Beran] p. 111. @)
    df-hodif @a |- -op = { <. <. f , g >. , h >. | ( ( f : ~H --> ~H /\
          g : ~H --> ~H ) /\ h = ( f +op ( -u 1 .op g ) ) ) } @.
$)

    $( Define the difference of two Hilbert space operators.  Definition of
       [Beran] p. 111. $)
    df-hodif $a |- -op = { <. <. f , g >. , h >. | ( ( f : ~H --> ~H /\
          g : ~H --> ~H ) /\
          h = { <. x , y >. | ( x e. ~H /\
           y = ( ( f ` x ) -h ( g ` x ) ) ) } ) } $.

    $( Define the sum of two Hilbert space functionals.  Definition of [Beran]
       p. 111.  Note that unlike some authors, we define a functional as any
       function from ` ~H ` to ` CC ` , not just linear (or bounded linear)
       ones. $)
    df-hfsum $a |- +fn = { <. <. f , g >. , h >. | ( ( f : ~H --> CC /\
          g : ~H --> CC ) /\
          h = { <. x , y >. | ( x e. ~H /\
           y = ( ( f ` x ) + ( g ` x ) ) ) } ) } $.

    $( Define the scalar product with a Hilbert space functional.  Definition
       of [Beran] p. 111. $)
    df-hfmul $a |- .fn = { <. <. f , g >. , h >. | ( ( f e. CC /\
          g : ~H --> CC ) /\
          h = { <. x , y >. | ( x e. ~H /\
           y = ( f x. ( g ` x ) ) ) } ) } $.
  $}

  ${
    $d f g h x y A $.  $d x y f g h S $.  $d x y f g h T $.
    $( Value of the sum of two Hilbert space operators. $)
    hosmval $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) -> ( S +op T ) =
        { <. x , y >. | ( x e. ~H /\ y = ( ( S ` x ) +h ( T ` x ) ) ) } ) $=
      ( vf vg vh chil wf cmap co wcel chos cv cfv cva wceq wa copab ax-hilex
      elmap opabex2 fveq1 opreq1d eqeq2d anbi2d opabbidv opreq2d copab2
      df-hosum anbi12i anbi1i oprabbii eqtr4i oprabval2 syl2anbr ) HHCICHHJKZLD
      UQLCDMKANZHLZBNZURCOZURDOZPKZQZRZABSZQHHDIHHCTTUAHHDTTUAEFGCDUQUQUSUTUREN
      ZOZURFNZOZPKZQZRZABSZVFMUSUTVAVJPKZQZRZABSABHVCTUBVGCQZVMVQABVRVLVPUSVRVK
      VOUTVRVHVAVJPURVGCUCUDUEUFUGVIDQZVQVEABVSVPVDUSVSVOVCUTVSVJVBVAPURVIDUCUH
      UEUFUGMHHVGIZHHVIIZRZGNVNQZRZEFGUIVGUQLZVIUQLZRZWCRZEFGUIABEFGUJWHWDEFGWG
      WBWCWEVTWFWAHHVGTTUAHHVITTUAUKULUMUNUOUP $.
      $( [9-Nov-2000] $)

    $( Value of the scalar product with a Hilbert space operator. $)
    hommval $p |- ( ( A e. CC /\ T : ~H --> ~H ) -> ( A .op T ) =
        { <. x , y >. | ( x e. ~H /\ y = ( A .h ( T ` x ) ) ) } ) $=
      ( vf vg vh chil wf cc wcel cmap co chot cv cfv csm wceq wa copab
      ax-hilex elmap opabex2 opreq1 eqeq2d anbi2d opabbidv fveq1 opreq2d
      copab2 df-homul anbi2i anbi1i oprabbii eqtr4i oprabval2 sylan2br ) HHDICJ
      KDHHLMZKCDNMAOZHKZBOZCUSDPZQMZRZSZABTZRHHDUAUAUBEFGCDJURUTVAEOZUSFOZPZQMZ
      RZSZABTZVFNUTVACVIQMZRZSZABTABHVCUAUCVGCRZVLVPABVQVKVOUTVQVJVNVAVGCVIQUDU
      EUFUGVHDRZVPVEABVRVOVDUTVRVNVCVAVRVIVBCQUSVHDUHUIUEUFUGNVGJKZHHVHIZSZGOVM
      RZSZEFGUJVSVHURKZSZWBSZEFGUJABEFGUKWFWCEFGWEWAWBWDVTVSHHVHUAUAUBULUMUNUOU
      PUQ $.
      $( [20-Feb-2006] $)

    $( Value of the difference of two Hilbert space operators. $)
    hodmval $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H ) -> ( S -op T ) =
        { <. x , y >. | ( x e. ~H /\ y = ( ( S ` x ) -h ( T ` x ) ) ) } ) $=
      ( vf vg vh chil wf cmap co wcel chod cv cfv cmv wceq wa copab ax-hilex
      elmap opabex2 fveq1 opreq1d eqeq2d anbi2d opabbidv opreq2d copab2
      df-hodif anbi12i anbi1i oprabbii eqtr4i oprabval2 syl2anbr ) HHCICHHJKZLD
      UQLCDMKANZHLZBNZURCOZURDOZPKZQZRZABSZQHHDIHHCTTUAHHDTTUAEFGCDUQUQUSUTUREN
      ZOZURFNZOZPKZQZRZABSZVFMUSUTVAVJPKZQZRZABSABHVCTUBVGCQZVMVQABVRVLVPUSVRVK
      VOUTVRVHVAVJPURVGCUCUDUEUFUGVIDQZVQVEABVSVPVDUSVSVOVCUTVSVJVBVAPURVIDUCUH
      UEUFUGMHHVGIZHHVIIZRZGNVNQZRZEFGUIVGUQLZVIUQLZRZWCRZEFGUIABEFGUJWHWDEFGWG
      WBWCWEVTWFWAHHVGTTUAHHVITTUAUKULUMUNUOUP $.
      $( [9-Nov-2000] $)

    $( Value of the sum of two Hilbert space functionals. $)
    hfsmval $p |- ( ( S : ~H --> CC /\ T : ~H --> CC ) -> ( S +fn T ) =
        { <. x , y >. | ( x e. ~H /\ y = ( ( S ` x ) + ( T ` x ) ) ) } ) $=
      ( vf vg vh chil cc wf cmap co wcel chfs cv cfv caddc wceq wa copab
      axcnex ax-hilex elmap opabex2 fveq1 opreq1d eqeq2d anbi2d opabbidv
      opreq2d copab2 df-hfsum anbi12i anbi1i oprabbii eqtr4i oprabval2
      syl2anbr ) HICJCIHKLZMDUSMCDNLAOZHMZBOZUTCPZUTDPZQLZRZSZABTZRHIDJIHCUAUBU
      CIHDUAUBUCEFGCDUSUSVAVBUTEOZPZUTFOZPZQLZRZSZABTZVHNVAVBVCVLQLZRZSZABTABHV
      EUBUDVICRZVOVSABVTVNVRVAVTVMVQVBVTVJVCVLQUTVICUEUFUGUHUIVKDRZVSVGABWAVRVF
      VAWAVQVEVBWAVLVDVCQUTVKDUEUJUGUHUINHIVIJZHIVKJZSZGOVPRZSZEFGUKVIUSMZVKUSM
      ZSZWESZEFGUKABEFGULWJWFEFGWIWDWEWGWBWHWCIHVIUAUBUCIHVKUAUBUCUMUNUOUPUQUR
      $.
      $( [23-May-2006] $)

    $( Value of the scalar product with a Hilbert space functional. $)
    hfmmval $p |- ( ( A e. CC /\ T : ~H --> CC ) -> ( A .fn T ) =
        { <. x , y >. | ( x e. ~H /\ y = ( A x. ( T ` x ) ) ) } ) $=
      ( vf vg vh chil cc wf wcel cmap co chft cv cfv cmul wceq wa copab axcnex
      ax-hilex elmap opabex2 opreq1 eqeq2d anbi2d opabbidv fveq1 opreq2d
      copab2 df-hfmul anbi2i anbi1i oprabbii eqtr4i oprabval2 sylan2br ) HIDJCI
      KDIHLMZKCDNMAOZHKZBOZCUTDPZQMZRZSZABTZRIHDUAUBUCEFGCDIUSVAVBEOZUTFOZPZQMZ
      RZSZABTZVGNVAVBCVJQMZRZSZABTABHVDUBUDVHCRZVMVQABVRVLVPVAVRVKVOVBVHCVJQUEU
      FUGUHVIDRZVQVFABVSVPVEVAVSVOVDVBVSVJVCCQUTVIDUIUJUFUGUHNVHIKZHIVIJZSZGOVN
      RZSZEFGUKVTVIUSKZSZWCSZEFGUKABEFGULWGWDEFGWFWBWCWEWAVTIHVIUAUBUCUMUNUOUPU
      QUR $.
      $( [23-May-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y S $.  $d x y T $.
    $( Value of the sum of two Hilbert space operators. $)
    hosval $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ A e. ~H ) ->
        ( ( S +op T ) ` A ) = ( ( S ` A ) +h ( T ` A ) ) ) $=
      ( vx vy chil wf wcel chos co cfv cva wceq wa cv copab hosmval fveq1d
      fveq2 opreq12d eqid oprex fvopab4 sylan9eq 3impa ) FFBGZFFCGZAFHZABCIJZKZ
      ABKZACKZLJZMUFUGNZUHUJADOZFHEOUOBKZUOCKZLJZMNDEPZKUMUNAUIUSDEBCQRDEAURUMF
      USUOAMUPUKUQULLUOABSUOACSTUSUAUKULLUBUCUDUE $.
      $( [13-Aug-2006] $) $( [10-Nov-2000] $)

    $( Value of the sum of two Hilbert space operators. $)
    hosvalOLD $p |- ( ( ( S : ~H --> ~H /\ T : ~H --> ~H ) /\ A e. ~H ) ->
        ( ( S +op T ) ` A ) = ( ( S ` A ) +h ( T ` A ) ) ) $=
      ( vx vy chil wf wa wcel chos co cfv cv cva wceq copab hosmval fveq1d
      fveq2 opreq12d eqid oprex fvopab4 sylan9eq ) FFBGFFCGHZAFIABCJKZLADMZFIEM
      UGBLZUGCLZNKZOHDEPZLABLZACLZNKZUEAUFUKDEBCQRDEAUJUNFUKUGAOUHULUIUMNUGABSU
      GACSTUKUAULUMNUBUCUD $.
      $( [10-Nov-2000] $)

    $( Value of the scalar product with a Hilbert space operator. $)
    homval $p |- ( ( A e. CC /\ T : ~H --> ~H /\ B e. ~H ) ->
        ( ( A .op T ) ` B ) = ( A .h ( T ` B ) ) ) $=
      ( vx vy cc wcel chil wf chot co cfv csm wceq wa cv copab hommval fveq1d
      fveq2 opreq2d eqid oprex fvopab4 sylan9eq 3impa ) AFGZHHCIZBHGZBACJKZLZAB
      CLZMKZNUGUHOZUIUKBDPZHGEPAUOCLZMKZNODEQZLUMUNBUJURDEACRSDEBUQUMHURUOBNUPU
      LAMUOBCTUAURUBAULMUCUDUEUF $.
      $( [20-Feb-2006] $)

    $( Value of the difference of two Hilbert space operators. $)
    hodval $p |- ( ( S : ~H --> ~H /\ T : ~H --> ~H /\ A e. ~H ) ->
        ( ( S -op T ) ` A ) = ( ( S ` A ) -h ( T ` A ) ) ) $=
      ( vx vy chil wf wcel chod co cfv cmv wceq wa cv copab hodmval fveq1d
      fveq2 opreq12d eqid oprex fvopab4 sylan9eq 3impa ) FFBGZFFCGZAFHZABCIJZKZ
      ABKZACKZLJZMUFUGNZUHUJADOZFHEOUOBKZUOCKZLJZMNDEPZKUMUNAUIUSDEBCQRDEAURUMF
      USUOAMUPUKUQULLUOABSUOACSTUSUAUKULLUBUCUDUE $.
      $( [28-Jun-2006] $) $( [10-Nov-2000] $)

    $( Value of the difference of two Hilbert space operators. $)
    hodvalOLD $p |- ( ( ( S : ~H --> ~H /\ T : ~H --> ~H ) /\ A e. ~H ) ->
        ( ( S -op T ) ` A ) = ( ( S ` A ) -h ( T ` A ) ) ) $=
      ( vx vy chil wf wa wcel chod co cfv cv cmv wceq copab hodmval fveq1d
      fveq2 opreq12d eqid oprex fvopab4 sylan9eq ) FFBGFFCGHZAFIABCJKZLADMZFIEM
      UGBLZUGCLZNKZOHDEPZLABLZACLZNKZUEAUFUKDEBCQRDEAUJUNFUKUGAOUHULUIUMNUGABSU
      GACSTUKUAULUMNUBUCUD $.
      $( [10-Nov-2000] $)

    $( Value of the sum of two Hilbert space functionals. $)
    hfsval $p |- ( ( ( S : ~H --> CC /\ T : ~H --> CC ) /\ A e. ~H ) ->
        ( ( S +fn T ) ` A ) = ( ( S ` A ) + ( T ` A ) ) ) $=
      ( vx vy chil cc wf wa wcel chfs co cfv cv caddc wceq copab hfsmval
      fveq1d fveq2 opreq12d eqid oprex fvopab4 sylan9eq ) FGBHFGCHIZAFJABCKLZMA
      DNZFJENUHBMZUHCMZOLZPIDEQZMABMZACMZOLZUFAUGULDEBCRSDEAUKUOFULUHAPUIUMUJUN
      OUHABTUHACTUAULUBUMUNOUCUDUE $.
      $( [23-May-2006] $)

    $( Value of the scalar product with a Hilbert space functional. $)
    hfmval $p |- ( ( A e. CC /\ T : ~H --> CC /\ B e. ~H ) ->
        ( ( A .fn T ) ` B ) = ( A x. ( T ` B ) ) ) $=
      ( vx vy cc wcel chil wf chft co cfv cmul wceq wa cv copab hfmmval fveq1d
      fveq2 opreq2d eqid oprex fvopab4 sylan9eq 3impa ) AFGZHFCIZBHGZBACJKZLZAB
      CLZMKZNUGUHOZUIUKBDPZHGEPAUOCLZMKZNODEQZLUMUNBUJURDEACRSDEBUQUMHURUOBNUPU
      LAMUOBCTUAURUBAULMUCUDUEUF $.
      $( [23-May-2006] $)
  $}

  $( Closure of the sum of two Hilbert space operators. $)
  hoscl $p |- ( ( ( S : ~H --> ~H /\ T : ~H --> ~H ) /\ A e. ~H ) ->
      ( ( S +op T ) ` A ) e. ~H ) $=
    ( chil wf wa wcel chos co cfv cva hosvalOLD ffvelrn anim12i anandirs
    hvaddcl syl eqeltrd ) DDBEZDDCEZFADGZFZABCHIJABJZACJZKIZDABCLUBUCDGZUDDGZFZ
    UEDGSTUAUHSUAFUFTUAFUGDDABMDDACMNOUCUDPQR $.
    $( [12-Nov-2000] $)

  $( Closure of the scalar product of a Hilbert space operator. $)
  homcl $p |- ( ( A e. CC /\ T : ~H --> ~H /\ B e. ~H ) ->
      ( ( A .op T ) ` B ) e. ~H ) $=
    ( cc wcel chil wf w3a chot co cfv csm homval wa ffvelrn anim2i 3impb
    hvmulcl syl eqeltrd ) ADEZFFCGZBFEZHZBACIJKABCKZLJZFABCMUDUAUEFEZNZUFFEUAUB
    UCUHUBUCNUGUAFFBCOPQAUERST $.
    $( [12-Oct-2006] $) $( [20-Feb-2006] $)

  $( Closure of the difference of two Hilbert space operators. $)
  hodcl $p |- ( ( ( S : ~H --> ~H /\ T : ~H --> ~H ) /\ A e. ~H ) ->
      ( ( S -op T ) ` A ) e. ~H ) $=
    ( chil wf wa wcel chod co cfv cmv hodvalOLD ffvelrn anim12i anandirs
    hvsubcl syl eqeltrd ) DDBEZDDCEZFADGZFZABCHIJABJZACJZKIZDABCLUBUCDGZUDDGZFZ
    UEDGSTUAUHSUAFUFTUAFUGDDABMDDACMNOUCUDPQR $.
    $( [15-Nov-2002] $)

