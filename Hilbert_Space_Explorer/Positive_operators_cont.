$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Positive operators (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

    $( Ordering relation for positive operators.  Definition of positive
       operator ordering in [Kreyszig] p. 470. $)
    leopg $p |- ( ( T e. A /\ U e. B ) -> ( T <_op U <-> ( ( U -op T ) e.
                HrmOp /\ A. x e. H~ 0 <_ ( ( ( U -op T ) ` x ) .ih x ) ) ) ) $=
      ( vu vt cv chod co cho wcel cc0 cfv csp cle wbr chil wral wa cleo wceq
      opreq2 eleq1d fveq1d opreq1d breq2d ralbidv anbi12d opreq1 df-leop brabg
      ) FHZGHZIJZKLZMAHZUONZUQOJZPQZARSZTUMDIJZKLZMUQVBNZUQOJZPQZARSZTEDIJZKLZM
      UQVHNZUQOJZPQZARSZTGFDEBCUAUNDUBZUPVCVAVGVNUOVBKUNDUMIUCZUDVNUTVFARVNUSVE
      MPVNURVDUQOVNUQUOVBVOUEUFUGUHUIUMEUBZVCVIVGVMVPVBVHKUMEDIUJZUDVPVFVLARVPV
      EVKMPVPVDVJUQOVPUQVBVHVQUEUFUGUHUIAFGUKUL $.
      $( [23-Jul-2006] $) $( [23-Jul-2006] $)

    $( Ordering relation for operators.  Definition of positive operator
       ordering in [Kreyszig] p. 470. $)
    leopt $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                A. x e. H~ 0 <_ ( ( ( U -op T ) ` x ) .ih x ) ) ) $=
      ( cho wcel wa cleo wbr chod co cc0 cv cfv csp cle chil wral leopg hmopdt
      ancoms biantrurd bitr4d ) BDEZCDEZFZBCGHCBIJZDEZKALZUFMUHNJOHAPQZFUIADDBC
      RUEUGUIUDUCUGCBSTUAUB $.
      $( [6-Aug-2006] $) $( [23-Jul-2006] $)

    $( Ordering relation for operators.  Definition of operator ordering in
       [Young] p. 141. $)
    leop2t $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                A. x e. H~ ( ( T ` x ) .ih x ) <_ ( ( U ` x ) .ih x ) ) ) $=
      ( cho wcel wa cleo wbr cc0 cv chod co cfv csp cle chil wral leopt cmin
      wf wceq cmv hodvalt 3com12 3expa opreq1d his2subt ffvelrn adantll
      adantlr pm3.27 syl3anc eqtrd hmopft anim12i sylan breq2d cr wb subge0t
      hmopret sylanc bitrd ralbidva ) BDEZCDEZFZBCGHIAJZCBKLMZVHNLZOHZAPQVHBMZV
      HNLZVHCMZVHNLZOHZAPQABCRVGVKVPAPVGVHPEZFZVKIVOVMSLZOHZVPVRVJVSIOPPBTZPPCT
      ZFZVQVJVSUAVGWCVQFZVJVNVLUBLZVHNLZVSWDVIWEVHNWAWBVQVIWEUAZWBWAVQWGVHCBUCU
      DUEUFVNPEZVLPEZVQWFVSUAWDVNVLVHUGWBVQWHWAPPVHCUHUIWAVQWIWBPPVHBUHUJWCVQUK
      ULUMVEWAVFWBBUNCUNUOUPUQVOUREZVMUREZVTVPUSVRVOVMUTVFVQWJVEVHCVAUIVEVQWKVF
      VHBVAUJVBVCVDVC $.
      $( [9-Aug-2006] $) $( [23-Jul-2006] $)

    $( Operator ordering in terms of a positive operator.  Definition of
       operator ordering in [Retherford] p. 49. $)
    leop3t $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                0hop <_op ( U -op T ) ) ) $=
      ( vx cho wcel wa cleo wbr cc0 cv chod co cfv csp cle chil wral ch0o
      leopt wb leop2t 0hmop a1i hmopdt ancoms sylanc c0v ho0valt opreq1d hi01t
      eqtrd breq1d ralbiia syl6rbb bitrd ) ADEZBDEZFZABGHICJZBAKLZMUSNLZOHZCPQZ
      RUTGHZCABSURVDUSRMZUSNLZVAOHZCPQZVCRDEZUTDEZVDVHTURCRUTUAVIURUBUCUQUPVJBA
      UDUEUFVGVBCPUSPEZVFIVAOVKVFUGUSNLIVKVEUGUSNUSUHUIUSUJUKULUMUNUO $.
      $( [22-Aug-2006] $) $( [23-Jul-2006] $)

    $( Binary relation defining a positive operator.  Definition VI.1 of
       [Retherford] p. 49. $)
    leoppost $p |- ( T e. HrmOp -> ( 0hop <_op T <->
                A. x e. H~ 0 <_ ( ( T ` x ) .ih x ) ) ) $=
      ( cho wcel ch0o cleo wbr cc0 cv chod co cfv csp cle chil wral wb 0hmop
      leopt mpan wf wceq hmopft hosubid1t syl fveq1d opreq1d breq2d ralbidv
      bitrd ) BCDZEBFGZHAIZBEJKZLZUMMKZNGZAOPZHUMBLZUMMKZNGZAOPECDUKULURQRAEBST
      UKUQVAAOUKUPUTHNUKUOUSUMMUKUMUNBUKOOBUAUNBUBBUCBUDUEUFUGUHUIUJ $.
      $( [24-Aug-2006] $) $( [25-Jul-2006] $)

    $( The ordering relation for operators is reflexive. $)
    leoprf2t $p |- ( T : H~ --> H~ -> T <_op T ) $=
      ( vx chil wf cleo wbr chod co cho wcel cc0 cv cfv csp cle wral wa ch0o
      hodidt 0hmop syl6eqel c0v wceq adantr fveq1d ho0valt adantl eqtrd
      opreq1d hi01t eqtr2d 0re leid syl5breq r19.21aiva jca cvv wb leopg
      ax-hilex fex mpan2 sylanc mpbird ) CCADZAAEFZAAGHZIJZKBLZVGMZVINHZOFZBCPZ
      QZVEVHVMVEVGRIASZTUAVEVLBCVEVICJZQZKVKKOVQVKUBVINHZKVQVJUBVINVQVJVIRMZUBV
      QVIVGRVEVGRUCVPVOUDUEVPVSUBUCVEVIUFUGUHUIVPVRKUCVEVIUJUGUKKULUMUNUOUPAUQJ
      ZVTVFVNURVEBUQUQAAUSVECUQJVTUTCCUQAVAVBZWAVCVD $.
      $( [23-Dec-2006] $) $( [24-Jul-2006] $)

    $( The ordering relation for operators is reflexive. $)
    leoprft $p |- ( T e. HrmOp -> T <_op T ) $=
      ( vx cho wcel cleo wbr cc0 cv chod co cfv csp cle chil wral wa c0v ch0o
      wceq wf hmopft hodidt syl adantr fveq1d ho0valt adantl eqtrd opreq1d
      hi01t eqtr2d 0re leid syl5breq r19.21aiva wb leopt anidms mpbird ) ACDZAA
      EFZGBHZAAIJZKZVBLJZMFZBNOZUTVFBNUTVBNDZPZGVEGMVIVEQVBLJZGVIVDQVBLVIVDVBRK
      ZQVIVBVCRUTVCRSZVHUTNNATVLAUAAUBUCUDUEVHVKQSUTVBUFUGUHUIVHVJGSUTVBUJUGUKG
      ULUMUNUOUTVAVGUPBAAUQURUS $.
      $( [7-Aug-2006] $) $( [23-Jul-2006] $)

    $( The square of a Hermitian operator is positive. $)
    leopsqt $p |- ( T e. HrmOp -> 0hop <_op ( T o. T ) ) $=
      ( vx cho wcel ch0o ccom cleo wbr cc0 cv cfv csp co cle chil wral wa wf
      ffvelrn hmopft sylan hiidge0t syl wceq hmopt pm3.26 pm3.27 syl3anc wfun
      fvco3 ffun adantr opreq1d eqtr4d breqtrd r19.21aiva wb eqid hmopcot
      mp3an3 anidms leoppost mpbird ) ACDZEAAFZGHZIBJZVEKZVGLMZNHZBOPZVDVJBOVDV
      GODZQZIVGAKZVNLMZVINVMVNODZIVONHOOARZVLVPVDOOVGASATZUAZVNUBUCVMVOVNAKZVGL
      MZVIVDVPVLVOWAUDVMVNVGAUEVDVLUFVSVDVLUGZUHVMVHVTVGLAUIZVQVLVHVTUDVMOOVGAA
      UJVDWCVLVDVQWCVROOAUKUCULVDVQVLVRULWBUHUMUNUOUPVDVECDZVFVKUQVDWDVDVDVEVEU
      DWDVEURAAUSUTVABVEVBUCVC $.
      $( [7-Sep-2006] $) $( [23-Aug-2006] $)

    $( The zero operator is a positive operator.  (The literature calls it
       "positive," even though in some sense it is really "nonnegative.")
       Part of Example 12.2(i) in [Young] p. 142. $)
    0leop $p |- 0hop <_op 0hop $=
      ( ch0o cho wcel cleo wbr 0hmop leoprft ax-mp ) ABCAADEFAGH $.
      $( [24-Aug-2006] $) $( [23-Jul-2006] $)

    $( The identity operator is a positive operator.  Part of Example 12.2(i)
       in [Young] p. 142. $)
    idleop $p |- 0hop <_op Iop $=
      ( vx ch0o chio cleo wbr cv cfv csp co cle chil cho wcel wral wb 0hmop
      idhmop leop2t mp2an cc0 hiidge0t c0v ho0valt opreq1d hi01t eqtrd hoivalt
      3brtr4d mprgbir ) BCDEZAFZBGZUKHIZUKCGZUKHIZJEZAKBLMCLMUJUPAKNOPQABCRSUKK
      MZTUKUKHIUMUOJUKUAUQUMUBUKHITUQULUBUKHUKUCUDUKUEUFUQUNUKUKHUKUGUDUHUI $.
      $( [10-Sep-2006] $) $( [23-Jul-2006] $)

    $( The sum of two positive operators is positive.  Exercise 1(i) of
       [Retherford] p. 49. $)
    leopaddt $p |- ( ( ( T e. HrmOp /\ U e. HrmOp ) /\ ( 0hop <_op T
               /\ 0hop <_op U ) ) -> 0hop <_op ( T +op U ) ) $=
      ( vx cho wcel wa ch0o cleo wbr chos co cc0 cv cfv csp cle chil wral
      caddc wi cr addge0t ex hmopret syl2an anandirs wf wceq cva hosvaltOLD
      opreq1d ax-his2 ffvelrn adantlr adantll pm3.27 syl3anc eqtrd hmopft
      anim12i sylan breq2d sylibrd r19.20dva r19.26 syl5ibr leoppost bi2anan9
      wb hmopst syl 3imtr4d imp ) ADEZBDEZFZGAHIZGBHIZFZGABJKZHIZVPLCMZANZWBOKZ
      PIZCQRZLWBBNZWBOKZPIZCQRZFZLWBVTNZWBOKZPIZCQRZVSWAVPWEWIFZCQRWOWKVPWPWNCQ
      VPWBQEZFZWPLWDWHSKZPIZWNVNVOWQWPWTTZWDUAEZWHUAEZXAVNWQFVOWQFXBXCFWPWTWDWH
      UBUCWBAUDWBBUDUEUFWRWMWSLPQQAUGZQQBUGZFZWQWMWSUHVPXFWQFZWMWCWGUIKZWBOKZWS
      XGWLXHWBOWBABUJUKWCQEZWGQEZWQXIWSUHXGWCWGWBULXDWQXJXEQQWBAUMUNXEWQXKXDQQW
      BBUMUOXFWQUPUQURVNXDVOXEAUSBUSUTVAVBVCVDWEWICQVEVFVNVQWFVOVRWJCAVGCBVGVHV
      PVTDEWAWOVIABVJCVTVGVKVLVM $.
      $( [17-Sep-2006] $) $( [25-Jul-2006] $)

    $( The scalar product of a nonnegative real and a positive operator is
       a positive operator.  Exercise 1(ii) of [Retherford] p. 49. $)
    leopmulit $p |- ( ( ( A e. RR /\ T e. HrmOp ) /\ ( 0 <_ A /\
                0hop <_op T ) ) -> 0hop <_op ( A .op T ) ) $=
      ( vx cr wcel cho wa cc0 cle wbr ch0o cleo chot co cv cfv csp chil wral
      wi cmul mulge0t simpll hmopret adantll jca sylan exp32 imp an1rs wb cc
      wf wceq csm homvalt 3expa opreq1d ax-his3 ffvelrn pm3.27 syl3anc eqtrd
      recnt hmopft anim12i breq2d adantlr sylibrd r19.20dva ex imp3a leoppost
      adantl anbi2d hmopmt syl 3imtr4d ) ADEZBFEZGZHAIJZKBLJZGZKABMNZLJZWAWBHCO
      ZBPZWGQNZIJZCRSZGHWGWEPZWGQNZIJZCRSZWDWFWAWBWKWOWAWBWKWOTWAWBGZWJWNCRWPWG
      REZGWJHAWIUANZIJZWNWAWQWBWJWSTZWAWQGZWBWTXAWBWJWSVSWIDEZGWBWJGWSXAAWIUBXA
      VSXBVSVTWQUCVTWQXBVSWGBUDUEUFUGUHUIUJWAWQWNWSUKWBXAWMWRHIAULEZRRBUMZGZWQW
      MWRUNWAXEWQGZWMAWHUONZWGQNZWRXFWLXGWGQXCXDWQWLXGUNAWGBUPUQURXCWHREZWQXHWR
      UNXFAWHWGUSXCXDWQUCXDWQXIXCRRWGBUTUEXEWQVAVBVCVSXCVTXDAVDBVEVFUGVGVHVIVJV
      KVLWAWCWKWBVTWCWKUKVSCBVMVNVOWAWEFEWFWOUKABVPCWEVMVQVRUI $.
      $( [10-Sep-2006] $) $( [25-Jul-2006] $)

    $( The scalar product of a positive real and a positive operator is
       a positive operator.  Exercise 1(ii) of [Retherford] p. 49. $)
    leopmult $p |- ( ( A e. RR /\ T e. HrmOp /\ 0 < A ) ->
                ( 0hop <_op T <-> 0hop <_op ( A .op T ) ) ) $=
      ( cr wcel cho cc0 clt wbr w3a ch0o cleo chot co wa cle leopmulit 3simpa
      adantr 0re ltlet 3impia mp3an1 3adant2 anim1i sylanc c1 cdiv anassrs wne
      gt0ne0t rerecclt syldan hmopmt 3adant3 jca recgt0t wi a1i mpd sylan wceq
      cmul cc recid2t recnt opreq1d chil wf homulasst recclt 3ad2ant1 hmopft
      3ad2ant2 syl3anc homulid2t syl 3eqtr3d breqtrd impbida ) ACDZBEDZFAGHZIZJ
      BKHZJABLMZKHZVTWANZFAOHZWDNWFWCWDNABPWCWGWDVTWAWBQRWCWHWDVTWBWHWAFCDZVTWB
      WHSWIVTWBWHFATUAUBUCUDUEWCWFNJUFAUGMZWELMZBKWJCDZWEEDZNZFWJOHZNWFJWKKHZWC
      WNWOWFWPWJWEPUHWCWNWOWCWLWMVTWBWLWAVTWBAFUIZWLAUJZAUKULZUCVTWAWMWBABUMUNU
      OVTWBWOWAVTWBNZFWJGHZWOAUPWIWLXAWOUQWTFWJTWIWTSURWSUEUSUCUOUTWCWKBVAWFWCW
      JAVBMZBLMZUFBLMZWKBVTWBXCXDVAWAWTXBUFBLAVCDZWQXBUFVAWTAVDVTXEWBAVEZRZWRUE
      VFUCWJVCDZXEVGVGBVHZXCWKVAWCWJABVIVTWBXHWAXEWQXHWTAVJXGWRUEUCVTWAXEWBXFVK
      WAVTXIWBBVLZVMVNWAVTXDBVAZWBWAXIXKXJBVOVPVMVQRVRVS $.
      $( [21-Sep-2006] $) $( [23-Aug-2006] $)

    $( Scalar product applied to operator ordering. $)
    leopmul2it $p |- ( ( ( A e. RR /\ T e. HrmOp /\ U e. HrmOp ) /\ ( 0 <_ A /\
                T <_op U ) ) -> ( A .op T ) <_op ( A .op U ) ) $=
      ( cr wcel cho w3a cc0 cle wbr cleo chot co wi wa ch0o chod leopmulit
      exp32 3simp1 hmopdt ancoms 3adant1 sylanc imp wb leop3t adantr hmopmt
      syl2an 3impdi wceq cc chil wf hosubdit recnt hmopft syl3an 3com23 breq2d
      bitr4d 3imtr4d ex imp32 ) ADEZBFEZCFEZGZHAIJZBCKJZABLMZACLMZKJZVIVJVKVNNV
      IVJOPCBQMZKJZPAVOLMZKJZVKVNVIVJVPVRNZVFVOFEZVJVSNVIVFVTOVJVPVRAVORSVFVGVH
      TVGVHVTVFVHVGVTCBUAUBUCUDUEVIVKVPUFZVJVGVHWAVFBCUGUCUHVIVNVRUFVJVIVNPVMVL
      QMZKJZVRVFVGVHVNWCUFZVLFEVMFEWDVFVGOVFVHOVLVMUGABUIACUIUJUKVIVQWBPKVFVHVG
      VQWBULZAUMEUNUNCUOUNUNBUOWEVFVHVGACBUPAUQCURBURUSUTVAVBUHVCVDVE $.
      $( [22-Sep-2006] $) $( [12-Aug-2006] $)

    $( The positive operator ordering relation satisfies trichotomy.
       Exercise 1(iii) of [Retherford] p. 49. $)
    leoptrit $p |- ( ( T e. HrmOp /\ U e. HrmOp ) ->
           ( ( T <_op U /\ U <_op T ) <-> T = U ) ) $=
      ( vx cho wcel wa cleo wbr cv cfv csp co cle chil wral wceq leop2t wb
      ancoms anbi12d cr letri3t hmopret adantlr adantll sylanc ralbidva r19.26
      syl6rbb clo lnopeqt hmoplint syl2an 3bitrd ) ADEZBDEZFZABGHZBAGHZFCIZAJUT
      KLZUTBJUTKLZMHZCNOZVBVAMHZCNOZFZVAVBPZCNOZABPZUQURVDUSVFCABQUPUOUSVFRCBAQ
      STUQVIVCVEFZCNOVGUQVHVKCNVAUAEZVBUAEZVHVKRUQUTNEZFVAVBUBUOVNVLUPUTAUCUDUP
      VNVMUOUTBUCUEUFUGVCVECNUHUIAUJEBUJEVIVJRUOUPCABUKAULBULUMUN $.
      $( [?] $) $( [25-Jul-2006] $)

    $( The positive operator ordering relation is transitive.  Exercise 1(iv)
       of [Retherford] p. 49. $)
    leoptrt $p |- ( ( ( S e. HrmOp /\ T e. HrmOp /\ U e. HrmOp ) /\
           ( S <_op T /\ T <_op U ) ) -> S <_op U ) $=
      ( vx cho wcel w3a cleo wbr wa cv cfv csp co cle chil wral wi cr letrt
      hmopret syl3an 3anandirs r19.20dva r19.26 syl5ibr wb leop2t 3adant3
      3adant1 anbi12d 3adant2 3imtr4d imp ) AEFZBEFZCEFZGZABHIZBCHIZJZACHIZURDK
      ZALVCMNZVCBLVCMNZOIZDPQZVEVCCLVCMNZOIZDPQZJZVDVHOIZDPQZVAVBURVFVIJZDPQVMV
      KURVNVLDPUOUPUQVCPFZVNVLRZVDSFVESFVHSFVPUOVOJUPVOJUQVOJVDVEVHTVCAUAVCBUAV
      CCUAUBUCUDVFVIDPUEUFURUSVGUTVJUOUPUSVGUGUQDABUHUIUPUQUTVJUGUODBCUHUJUKUOU
      QVBVMUGUPDACUHULUMUN $.
      $( [?] $) $( [25-Jul-2006] $)

    $( A bounded Hermitian operator is less than or equal to its norm times the
       identity operator. $)
    leopnmidt $p |- ( ( T e. HrmOp /\ ( normop ` T ) e. RR ) ->
                    T <_op ( ( normop ` T ) .op Iop ) ) $=
      ( vx cho wcel cnop cfv cr wa chio chot co cleo wbr cv csp cle chil wral
      cabs hmopret adantlr cc recnd absclt syl idhmop hmopmt mpan2 sylan
      adantll leabst cno cmul axmulrcl wf ffvelrn normclt hmopft adantl sylanc
      bcst pm3.27 w3a cc0 lemul1itOLD syl2an 3jca normge0t cbo nmbdoplbt clo
      elbdop2t biimpr hmoplint jca csm wceq c2 cexp sqvalt normsqt eqtr3d
      opreq2d axmulass recnt ad2antlr syl3anc ax-his3 3eqtr4d homvalt wf1o
      hoif f1of ax-mp a1i hoivalt eqtrd opreq1d eqtr4d breqtrd letrd
      r19.21aiva wb leop2t sylan2 mpbird ) ACDZAEFZGDZHZAXHIJKZLMZBNZAFZXMOKZXM
      XKFZXMOKZPMZBQRZXJXRBQXJXMQDZHZXOXOSFZXQXGXTXOGDZXIXMATZUAXGXTYBGDZXIXGXT
      HZXOUBDYEYFXOYDUCXOUDUEUAZXIXTXQGDZXGXKCDZXTYHXIXMXKTXIICDYIUFXHIUGUHZUIU
      JZXGXTXOYBPMZXIYFYCYLYDXOUKUEUAYAYBXNULFZXMULFZUMKZXQYGYMGDZYNGDZYOGDYAYM
      YNUNXGXTYPXIQQAUOZXTYPXGYRXTHXNQDZYPQQXMAUPZXNUQUEAURZUIUAZXTYQXJXMUQZUSZ
      UTYKXGXTYBYOPMZXIYSXTUUEYFXNXMVAYRXTYSXGYTUUAUIXGXTVBUTUAYAYOXHYNUMKZYNUM
      KZXQPYPUUFGDZYQVCVDYNPMZYMUUFPMZHYOUUGPMYAYMUUFYNVEYAYPUUHYQUUBXIYQUUHXJX
      TXHYNUNXGXIVBUUCVFUUDVGYAUUIUUJXTUUIXJXMVHUSAVIDZXTUUJXJXMAVJAVKDZXIUUKXG
      UUKUULXIHAVLVMAVNUIUIVOUTYAUUGXHXMVPKZXMOKZXQYAXHYNYNUMKZUMKZXHXMXMOKZUMK
      ZUUGUUNXTUUPUURVQXJXTUUOUUQXHUMXTYNVRVSKZUUOUUQXTYNUBDZUUSUUOVQXTYNUUCUCY
      NVTUEXMWAWBWCUSXHUBDZUUTUUTUUGUUPVQYAXHYNYNWDXIUVAXGXTXHWEWFZYAYNUUDUCZUV
      CWGUVAXTXTUUNUURVQYAXHXMXMWHUVBXJXTVBZUVDWGWIYAXPUUMXMOYAXPXHXMIFZVPKZUUM
      UVAQQIUOZXTXPUVFVQYAXHXMIWJUVBUVGYAQQIWKUVGWLQQIWMWNWOUVDWGXTUVFUUMVQXJXT
      UVEXMXHVPXMWPWCUSWQWRWSWTXAXAXBXGYIXLXSXCXIBAXKXDYJXEXF $.
      $( [12-Aug-2006] $) $( [11-Aug-2006] $)

    $( A nonzero, bounded Hermitian operator divided by its norm is less than
       or equal to the identity operator. $)
    nmopleidt $p |- ( ( T e. HrmOp /\ ( normop ` T ) e. RR /\ T =/= 0hop ) ->
                    ( ( 1 / ( normop ` T ) ) .op T ) <_op Iop ) $=
      ( cho wcel cnop cfv cr ch0o wne c1 cdiv co chot chio cleo wbr wa cc0 clo
      nmlnopne0t biimpar hmoplint sylan adantlr w3a cle leopmul2it rerecclt
      adantll simpll idhmop hmopmt mpan2 ad2antlr 3jca clt recgt0t simplr chil
      wf nmopgt0t biimpa hmopft sylanc wi 0re ltlet mpan syl mpd leopnmidt
      adantr jca wceq cc cmul wf1o hoif f1of ax-mp homulasst mp3an3 recclt
      pm3.26 recid2t opreq1d eqtr3d homulid2t syl6eq recnt breqtrd syldan
      3impa ) ABCZADEZFCZAGHZIWNJKZALKZMNOZWMWOPZWPWNQHZWSWMWPXAWOARCZWPXAWMXBX
      AWPASTAUAUBUCWTXAPZWRWQWNMLKZLKZMNWQFCZWMXDBCZUDQWQUEOZAXDNOZPWRXENOXCWQA
      XDUFXCXFWMXGWOXAXFWMWNUGZUHWMWOXAUIWOXGWMXAWOMBCXGUJWNMUKULUMUNXCXHXIXCQW
      QUOOZXHWOQWNUOOZXKXCWNUPWMWOXAUQWMXAXLWOURURAUSZXAXLWMXMXAXLAUTVAAVBUBUCV
      CWOXAXKXHVDZWMWOXAPXFXNXJQFCXFXNVEQWQVFVGVHUHVIWTXIXAAVJVKVLVCWOXAXEMVMZW
      MWNVNCZXAXOWOXPXAPZXEIMLKZMXQWQWNVOKZMLKZXEXRWQVNCZXPXTXEVMZXQYAXPURURMUS
      ZYBURURMVPYCVQURURMVRVSZWQWNMVTWAWNWBXPXAWCVCXQXSIMLWNWDWEWFYCXRMVMYDMWGV
      SWHWNWIUBUHWJWKWL $.
      $( [22-Sep-2006] $) $( [12-Aug-2006] $)
  $}

  ${
    $d u v T $.
    opsqrlem1.1 $e |- T e. HrmOp $.
    opsqrlem1.2 $e |- ( normop ` T ) e. RR $.
    opsqrlem1.3 $e |- 0hop <_op T $.
    opsqrlem1.4 $e |- R = ( ( 1 / ( normop ` T ) ) .op T ) $.
    opsqrlem1.5 $e |- ( T =/= 0hop -> E. u e. HrmOp ( 0hop <_op u /\
                      ( u o. u ) = R ) ) $.
    $( Lemma for ~ opsqr . $)
    opsqrlem1 $p |- ( T =/= 0hop ->
                   E. v e. HrmOp ( 0hop <_op v /\ ( v o. v ) = T ) ) $=
      ( ch0o wne cv cleo wbr ccom wceq wa cho wrex wcel cnop cfv csqr chot co
      breq2 coeq1 coeq2 eqtrd eqeq1d anbi12d rcla4ev cr cc0 cle chil wf hmopft
      ax-mp nmopge0t sqrcl hmopmt mpan ad2antlr sqrge0 leopmulit mpanr1 mpanl1
      ad2ant2lr cc recn homco1t mp3an1 homulclt syl sylanc clo homco2t
      hmoplint opreq2d cmul fco homulasst mp3an12 sqrth opreq1i syl5reqr
      3eqtrd c1 cdiv id syl6eq wb nmlnopne0t recidz sylbir opreq1d rerecclz
      recnd mp3an13 homulid2t a1i 3eqtr3d sylan9eqr adantlr adantrl jca exp31
      r19.23adv mpd ) DJKZJBLZMNZXLXLOZCPZQZBRSJALZMNZXQXQOZDPZQZARSZIXKXPYBBRX
      KXLRTZXPYBDUAUBZUCUBZXLUDUEZRTZJYFMNZYFYFOZDPZQZYBXKYCQZXPQZYAYKAYFRXQYFP
      ZXRYHXTYJXQYFJMUFYNXSYIDYNXSYFXQOYIXQYFXQUGXQYFYFUHUIUJUKULYCYGXKXPYEUMTZ
      YCYGUNYDUONZYOUPUPDUQZYPDRTZYQEDURUSZDUTUSZYDFVAUSZYEXLVBVCVDYMYHYJYCXMYH
      XKXOYOYCXMYHUUAYOYCQUNYEUONZXMYHYPUUBYTYDFVEUSYEXLVFVGVHVIYLXOYJXMYLXOQYI
      YDXNUDUEZDYCYIUUCPXKXOYCYIYEXLYFOZUDUEZYEYEXNUDUEZUDUEZUUCUPUPXLUQZUPUPYF
      UQZYIUUEPZYCYEVJTZUUHUUIUUJYEUUAVKZYEXLYFVLVMXLURZYCUUHUUIUUMUUKUUHUUIUUL
      YEXLVNVCVOVPYCUUDUUFYEUDXLVQTZUUHUUDUUFPZYCUUKUUNUUHUUOUULYEXLXLVRVMXLVSU
      UMVPVTYCYEYEWAUEZXNUDUEZUUGUUCYCUPUPXNUQZUUQUUGPZUUHUUHUURYCUPUPUPXLXLWBU
      UMUUMVPUUKUUKUURUUSUULUULYEYEXNWCWDVOUUPYDXNUDYPUUPYDPYTYDFWEUSWFWGWHVDXK
      XOUUCDPYCXOXKUUCYDWIYDWJUEZDUDUEZUDUEZDXOXNUVAYDUDXOXNCUVAXOWKHWLVTXKYDUU
      TWAUEZDUDUEZWIDUDUEZUVBDXKUVCWIDUDXKYDUNKZUVCWIPDVQTZUVFXKWMYRUVGEDVSUSDW
      NUSZYDYDFVKZWOWPWQXKUUTVJTZUVDUVBPZXKUUTXKUVFUUTUMTUVHYDFWRWPWSYDVJTUVJYQ
      UVKUVIYSYDUUTDWCWTVOUVEDPZXKYQUVLYSDXAUSXBXCXDXEUIXFXGVPXHXIXJ $.
      $( [?] $) $( [9-Aug-2006] $)
  $}

  ${
    $d v w G $.  $d v H $.  $d v w S $.  $d v w x y z T $.
    $d j k F $.  $d j k N $.
    opsqrlem2.1 $e |- T e. HrmOp $.
    opsqrlem2.2 $e |- S = { <. <. x , y >. , z >. | ( ( x e. HrmOp /\
    y e. HrmOp ) /\ z = ( x +op ( ( 1 / 2 ) .op ( T -op ( x o. x ) ) ) ) ) } $.
    opsqrlem2.3 $e |- F = ( S seq1 ( NN X. { 0hop } ) ) $.
    $( Lemma for ~ opsqr .  ` F `` N ` is the recursive function A_n (starting
       at n=1 instead of 0) of Theorem 9.4-2 of [Kreyszig] p. 476. $)
    opsqrlem2 $p |- ( F ` 1 ) = 0hop $=
      ( c1 cfv cn ch0o csn cxp cseq1 co fveq1i cho cv c2 cdiv ccom chod chot
      chos hmopex oprabex2 nnex snex xpex seq11 wcel wceq 1nn 0hmop elisseti
      fvconst2 ax-mp 3eqtr ) JFKJDLMNZOZPQZKJVBKZMJFVCIRDVBABCSSATZJUAUBQEVEVEU
      CUDQUEQUFQDUGUGHUHLVAUIMUJUKULJLUMVDMUNUOLMJMSUPUQURUSUT $.
      $( [?] $) $( [17-Aug-2006] $)

    $( Lemma for ~ opsqr . $)
    opsqrlem3 $p |- F : NN --> HrmOp $=
      ( cn cho wf ch0o csn cxp cseq1 co wss 0hmop elisseti fconst wcel snssi
      ax-mp fss mp2an cv c1 c2 cdiv ccom chod chot chos wceq eqid hmopcot
      mp3an3 anidms hmopdt mpan cr 2re 2ne0 rereccl hmopmt 3syl hmopst mpdan
      adantr foprab hmopex oprabex2 nnex snex xpex seq1f wb feq1 mpbir ) JKFLZJ
      KDJMNZOZPQZLZJKWCLZKKOKDLWEJWBWCLWBKRZWFJMMKSTUAMKUBWGSMKUCUDJWBKWCUEUFAB
      CKKAUGZUHUIUJQZEWHWHUKZULQZUMQZUNQZKDHWHKUBZWMKUBZBUGKUBWNWLKUBZWOWNWJKUB
      ZWKKUBZWPWNWQWNWNWJWJUOWQWJUPWHWHUQURUSEKUBWQWRGEWJUTVAWIVBUBWRWPUIVCVDVE
      WIWKVFVAVGWHWLVHVIVJVKKDWCABCKKWMDVLVLHVMJWBVNMVOVPVQUFFWDUOWAWEVRIJKFWDV
      SUDVT $.
      $( [?] $) $( [17-Aug-2006] $)

    $( Lemma for ~ opsqr . $)
    opsqrlem4 $p |- ( ( G e. HrmOp /\ H e. HrmOp ) ->
              ( G S H ) = ( G +op ( ( 1 / 2 ) .op ( T -op ( G o. G ) ) ) ) ) $=
      ( vw vv cv co c1 c2 cdiv ccom chod chot chos wceq cho opreq1 id coeq1
      coeq2 eqtrd opreq2d opreq12d eqeq12d opreq2 eqeq1d oprex eqid a1i
      oprabval2 vtocl2ga ) LNZMNZDOZUTPQROZEUTUTSZTOZUAOZUBOZUCGVADOZGVCEGGSZTO
      ZUAOZUBOZUCGHDOZVLUCLMGHUDUDUTGUCZVBVHVGVLUTGVADUEVNUTGVFVKUBVNUFVNVEVJVC
      UAVNVDVIETVNVDGUTSVIUTGUTUGUTGGUHUIUJUJUKULVAHUCVHVMVLVAHGDUMUNABCUTVAUDU
      DANZVCEVOVOSZTOZUAOZUBOVGDVGUTVFUBUOVOUTUCZVOUTVRVFUBVSUFVSVQVEVCUAVSVPVD
      ETVSVPUTVOSVDVOUTVOUGVOUTUTUHUIUJUJUKVGVGUCBNVAUCVGUPUQJURUS $.
      $( [?] $) $( [22-Aug-2006] $)

    $( Lemma for ~ opsqr . $)
    opsqrlem5 $p |- ( N e. NN -> ( F ` ( N + 1 ) ) =
  ( ( F ` N ) +op ( ( 1 / 2 ) .op ( T -op ( ( F ` N ) o. ( F ` N ) ) ) ) ) ) $=
      ( cn wcel c1 caddc co cfv ch0o csn cxp cseq1 c2 cdiv ccom chod chot chos
      wceq fveq1i a1i cho cv hmopex oprabex2 nnex snex xpex seq1p1 opsqrlem4
      opsqrlem3 ffvelrni peano2nn 0hmop elisseti fvconst2 syl syl6eqel sylanc
      opreq1i syl5eqr 3eqtrd ) GKLZGMNOZFPZVLDKQRZSZTOZPZGVPPZVLVOPZDOZGFPZMUAU
      BOZEWAWAUCUDOUEOUFOZVMVQUGVKVLFVPJUHUIGDVOABCUJUJAUKZWBEWDWDUCUDOUEOUFODU
      LULIUMKVNUNQUOUPUQVKWAVSDOZWCVTWAUJLVSUJLWEWCUGVKABCDEFWAVSHIJURKUJGFABCD
      EFHIJUSUTVKVSQUJVKVLKLVSQUGGVAKQVLQUJVBVCVDVEVBVFVGWAVRVSDGFVPJUHVHVIVJ
      $.
      $( [?] $) $( [17-Aug-2006] $)

    ${
      opsqrlem6.4 $e |- T <_op Iop $.
      $( Lemma for ~ opsqr .  _Warning:  The HTML proof page is 0.6 megabyte
       in size_. $)
      opsqrlem6 $p |- ( N e. NN -> ( F ` N ) <_op Iop ) $=
        ( vj vk cv cfv chio cleo wbr c1 caddc co wceq fveq2 breq1d ch0o
        opsqrlem2 idleop eqbrtr cn wcel chod c2 chot ccom chos cho wb idhmop
        leop3t mp2an mpbi wa hmopdt leopaddt mpanl2 mpanr2 eqid hmopcot mp3an3
        opsqrlem3 ffvelrni mpan syl sylanc leopsqt 3syl cdiv chil wf cc 2cn
        hoadddit mp3an1 hmopft fco ax-mp hosubclt 2ne0 reccl homulclt cmul
        homulasst mp3an12 homulid2t recid opreq1i syl5eq eqtr3d opreq2d eqtrd
        hoaddclt hosubdit ho2timest a1i hoaddsubasst hoadd23t mp3an13 3eqtr4rd
        opreq1d mp3an23 hosubsub4t 3eqtr3d hosubadd4t mpanr1 mpanl1 3eqtr2d
        hocsubdirt clo hmoplint hoddit hoid1 hoico2t opreq12d mp3an2 hoico1t
        jctil jca 3eqtrd opsqrlem5 3eqtr4d breqtrd peano2nn cr cc0 clt 2re
        2pos leopmult mpbird mpan2 nn1suc ) LNZFOZPQRSFOZPQRMNZSTUAZFOZPQRZGFOZ
        PQRLMGUUBSUBUUCUUDPQUUBSFUCUDUUBUUFUBUUCUUGPQUUBUUFFUCUDUUBGUBUUCUUIPQU
        UBGFUCUDUUDUEPQABCDEFHIJUFUGUHUUEUIUJZUUHUEPUUGUKUAZQRZUUJUULUEULUUKUMU
        AZQRZUUJUEPUUEFOZUKUAZUUPUNZPEUKUAZUOUAZUUMQUUQUPUJZUEUUQQRZUEUUSQRZUUJ
        UUTUVAUEUURQRZUVBEPQRZUVCKEUPUJZPUPUJZUVDUVCUQHUREPUSUTVAUUTUURUPUJZUVA
        UVCVBUVBUVFUVEUVGURHPEVCUTUUQUURVDVEVFUUPUPUJZUVHUUTUUJUVHUVHUUQUUQUBUU
        TUUQVGUUPUUPVHVIUUJUUOUPUJZUVHUIUPUUEFABCDEFHIJVJZVKZUVFUVIUVHURPUUOVCV
        LZVMZUVMVNUUJUVIUVHUVAUVKUVLUUPVOVPVNUUJPUUOUUOUNZULUUOUMUAZUKUAZUOUAZU
        URUOUAZULPUUOSULVQUAZEUVNUKUAZUMUAZUOUAZUKUAZUMUAZUUSUUMUUJULPUMUAZULUW
        BUMUAZUKUAZUWEUVOUVTUOUAZUKUAZUWDUVRUUJUWFUWHUWEUKUUJUWFUVOULUWAUMUAZUO
        UAZUWHVRVRUUOVSZVRVRUWAVSZUWFUWKUBZUUJULVTUJZUWLUWMUWNWAULUUOUWAWBWCUUJ
        UVIUWLUVKUUOWDZVMZUUJVRVRUVNVSZVRVRUVTVSZUWMUWLUWLUWRUUJVRVRVRUUOUUOWEU
        WQUWQVNZVRVREVSZUWRUWSUVEUXAHEWDWFZEUVNWGVLZUVSVTUJZUWSUWMULWAWHWIZUVSU
        VTWJVLVPZVNUUJUWJUVTUVOUOUUJULUVSWKUAZUVTUMUAZUWJUVTUUJUWRUWSUXHUWJUBZU
        WTUXCUWOUXDUWSUXIWAUXEULUVSUVTWLWMVPUUJSUVTUMUAZUVTUXHUUJUWRUWSUXJUVTUB
        UWTUXCUVTWNVPUXGSUVTUMULWAWHWOWPWQWRWSWTWSUUJVRVRUWBVSZUWDUWGUBZUWLUWMU
        XKUUJUUOUWAXAUWQUXFVNUWOVRVRPVSZUXKUXLWAUVFUXMURPWDWFZULPUWBXBWMVMUUJUV
        RUWEUVNUOUAZUVOEUOUAUKUAZUWEUVOUKUAUVTUKUAZUWIUUJUVQPUOUAZEUKUAZUXOUVOU
        KUAZEUKUAZUVRUXPUUJUXRUXTEUKUUJUWEUVPUOUAZPPUOUAZUVPUOUAZUXTUXRUYBUYDUB
        UUJUWEUYCUVPUOUXMUWEUYCUBUXNPXCWFWPXDUWRVRVRUVOVSZUXTUYBUBZUUJVRVRUWEVS
        ZUWRUYEUYFUWOUXMUYGWAUXNULPWJUTZUWEUVNUVOXEWCUWTUUJUVIUWLUYEUVKUWPUWOUW
        LUYEWAULUUOWJVLVPZVNUUJVRVRUVPVSZUXRUYDUBZUWRUYEUYJUUJUVNUVOWGUWTUYIVNZ
        UXMUYJUXMUYKUXNUXNPUVPPXFXGVMXHXIUUJUYJVRVRUVQVSZUXSUVRUBZUYLUXMUYJUYMU
        XNPUVPXAVLUYMUXMUXAUYNUXNUXBUVQPEXEXJVPVRVRUXOVSZUYEUYAUXPUBZUUJUYOUYEU
        XAUYPUXBUXOUVOEXKVIUUJUWRUYOUWTUYGUWRUYOUYHUWEUVNXAVLVMUYIVNXLUYEUWRUXQ
        UXPUBZUUJUYGUYEUWRUYQUYHUYGUYEVBUXAUWRUYQUXBUWEUVOEUVNXMXNXOUYIUWTVNUYE
        UWSUXQUWIUBZUUJUYGUYEUWSUYRUYHUWEUVOUVTXKWCUYIUUJUWRUWSUWTUXCVMVNXPXHUU
        JUUQUVQUURUOUUJUUQPUUPUNZUUOUUPUNZUKUAZPUVNUOUAZUUOUUOUOUAZUKUAZUVQUWLV
        RVRUUPVSZUUQVUAUBZUUJUXMUWLVUEVUFUXNPUUOUUPXQWCUWQUUJUVIUWLVUEUVKUWPUXM
        UWLVUEUXNPUUOWGVLVPVNUUJVUAUUPUUOUVNUKUAZUKUAZVUDUUJUYSUUPUYTVUGUKUUJUY
        SPPUNZPUUOUNZUKUAZUUPUUJUVIUWLUYSVUKUBZUVKUWPPXRUJZUXMUWLVULUVFVUMURPXS
        WFUXNPPUUOXTWMVPUUJVUIPVUJUUOUKVUIPUBUUJPUXNYAXDUUJUVIUWLVUJUUOUBUVKUWP
        UUOYBVPYCWTUUJUYTUUOPUNZUVNUKUAZVUGUUOXRUJZUWLUYTVUOUBZUUJVUPUXMUWLVUQU
        XNUUOPUUOXTYDUUJUVIVUPUVKUUOXSVMUWQVNUUJVUNUUOUVNUKUUJUVIUWLVUNUUOUBUVK
        UWPUUOYEVPXIWTYCUXMUWLVBUWLUWRVBVUHVUDUBUUJPUUOUUOUVNXMUUJUWLUXMUWQUXNY
        FUUJUWLUWRUWQUWTYGVNWTUUJVUBUVOUKUAZVUDUVQUUJUVOVUCVUBUKUUJUVIUWLUVOVUC
        UBUVKUWPUUOXCVPWSUWRUYEVURUVQUBZUUJUXMUWRUYEVUSUXNPUVNUVOXEWCUWTUYIVNWR
        YHXIUUJUUKUWCULUMUUJUUGUWBPUKABCDEFUUEHIJYIWSWSYJYKUUJUUGUPUJZUUKUPUJZU
        ULUUNUQZUUJUUFUIUJZVUTUUEYLZUIUPUUFFUVJVKZVMUVFVUTVVAURPUUGVCVLULYMUJVV
        AYNULYORVVBYPYQULUUKYRXGVPYSUUJVVCVUTUUHUULUQZVVDVVEVUTUVFVVFURUUGPUSYT
        VPYSUUA $.
        $( [?] $) $( [23-Aug-2006] $)
    $}
  $}

  ${
    opsqrlem8.1 $e |- T e. HrmOp $.
    opsqrlem8.2 $e |- 0hop <_op T $.
    opsqrlem8.3 $e |- T <_op Iop $.
    $( Lemma for ~ opsqr . $)
    opsqrlem8 $p |- E. u e. HrmOp ( 0hop <_op u /\
                    ( u o. u ) = T ) $=
      (  ) ? $.
      $( [?] $) $( [17-Aug-2006] $)
  $}

  ${
    $d u T $.
    $( Lemma for ~ opsqr . $)
    opsqrlem9 $p |- ( ( T e. HrmOp /\ 0hop <_op T /\ T <_op Iop ) ->
                 E. u e. HrmOp ( 0hop <_op u /\ ( u o. u ) = T ) ) $=
      ( cho wcel ch0o cleo wbr chio w3a cv ccom wceq wa wrex cif eqeq2 anbi2d
      rexbidv eleq1 breq2 breq1 3anbi123d 0hmop 0leop idleop 3pm3.2i elimhyp
      3simp1i 3simp2i 3simp3i opsqrlem8 dedth ) BCDZEBFGZBHFGZIZEAJZFGZUQUQKZBL
      ZMZACNURUSUPBEOZLZMZACNBEBVBLZVAVDACVEUTVCURBVBUSPQRAVBVBCDZEVBFGZVBHFGZU
      PVFVGVHIECDZEEFGZEHFGZIBEVEUMVFUNVGUOVHBVBCSBVBEFTBVBHFUAUBEVBLVIVFVJVGVK
      VHEVBCSEVBEFTEVBHFUAUBVIVJVKUCUDUEUFUGZUHVFVGVHVLUIVFVGVHVLUJUKUL $.
      $( [?] $) $( [8-Aug-2006] $)
  $}

  ${
    $d u v T $.
    opsqr.1 $e |- T e. HrmOp $.
    opsqr.2 $e |- ( normop ` T ) e. RR $.
    opsqr.3 $e |- 0hop <_op T $.
    $( Square root lemma. $)
    opsqr $p |- E. u e. HrmOp ( 0hop <_op u /\
                    ( u o. u ) = T ) $=
      ( vv ch0o wceq cv cleo wbr ccom wa cho wrex eqid eqeq2 anbi2d rexbidv
      wne c1 cnop cfv cdiv co chot wcel chio opsqrlem9 cr cc0 clo hmoplint
      ax-mp nmlnop0HIL biimp necon3i rerecclz syl hmopmt mpan2 cle leopmulit
      mpanr2 mpanl2 clt wi 0re ltlet mpan nmlnopgt0 recgt0 sylbi sylc sylanc
      nmopleidt mp3an12 syl3anc opsqrlem1 adantl 0hmop 0leop ho0f ho0co pm3.2i
      breq2 coeq1 coeq2 eqtrd eqeq1d anbi12d rcla4ev mp2an a1i pm2.61ne ) GGHZG
      AIZJKZWQWQLZBHZMZANOZGPWPXBWRWSGHZMZANOZBGBGHZXAXDANXFWTXCWRBGWSQRSBGTZXB
      WPAFUABUBUCZUDUEZBUFUEZBCDEXJPXJNUGZGXJJKZXJUHJKZGFIZJKXNXNLXJHMFNOXGFXJU
      IXGXIUJUGZXKXGXHUKTXOXHUKBGXHUKHXFBBNUGZBULUGCBUMUNZUOUPUQXHDURUSZXOXPXKC
      XIBUTVAUSXOUKXIVBKZXLXGXOXPXSXLCXOXPMXSGBJKXLEXIBVCVDVEXRXOUKXIVFKZXSXGUK
      UJUGXOXTXSVGVHUKXIVIVJXRXGUKXHVFKXTBXQVKXHDVLVMVNVOXPXHUJUGXGXMCDBVPVQVRV
      SVTXEWPGNUGGGJKZGGLZGHZMZXEWAYAYCWBGWCWDWEXDYDAGNWQGHZWRYAXCYCWQGGJWFYEWS
      YBGYEWSGWQLYBWQGWQWGWQGGWHWIWJWKWLWMWNWOUN $.
      $( [?] $) $( [8-Aug-2006] $)
  $}

