$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Positive operators (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

    $( Ordering relation for positive operators.  Definition of positive
       operator ordering in [Kreyszig] p. 470. $)
    leopg $p |- ( ( T e. A /\ U e. B ) -> ( T <_op U <-> ( ( U -op T ) e.
                HrmOp /\ A. x e. ~H 0 <_ ( ( ( U -op T ) ` x ) .ih x ) ) ) ) $=
      ( vu vt cv chod co cho wcel cc0 cfv csp cle wbr chil wral wa cleo wceq
      opreq2 eleq1d fveq1d opreq1d breq2d ralbidv anbi12d opreq1 df-leop brabg
      ) FHZGHZIJZKLZMAHZUONZUQOJZPQZARSZTUMDIJZKLZMUQVBNZUQOJZPQZARSZTEDIJZKLZM
      UQVHNZUQOJZPQZARSZTGFDEBCUAUNDUBZUPVCVAVGVNUOVBKUNDUMIUCZUDVNUTVFARVNUSVE
      MPVNURVDUQOVNUQUOVBVOUEUFUGUHUIUMEUBZVCVIVGVMVPVBVHKUMEDIUJZUDVPVFVLARVPV
      EVKMPVPVDVJUQOVPUQVBVHVQUEUFUGUHUIAFGUKUL $.
      $( [23-Jul-2006] $)

    $( Ordering relation for operators.  Definition of positive operator
       ordering in [Kreyszig] p. 470. $)
    leop $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                A. x e. ~H 0 <_ ( ( ( U -op T ) ` x ) .ih x ) ) ) $=
      ( cho wcel wa cleo wbr chod co cc0 cv cfv csp cle chil wral leopg hmopd
      ancoms biantrurd bitr4d ) BDEZCDEZFZBCGHCBIJZDEZKALZUFMUHNJOHAPQZFUIADDBC
      RUEUGUIUDUCUGCBSTUAUB $.
      $( [23-Jul-2006] $)

    $( Ordering relation for operators.  Definition of operator ordering in
       [Young] p. 141. $)
    leop2 $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                A. x e. ~H ( ( T ` x ) .ih x ) <_ ( ( U ` x ) .ih x ) ) ) $=
      ( cho wcel wa cleo wbr cc0 cv chod co cfv csp cle chil wral leop cmin wf
      wceq cmv hodval 3com12 3expa opreq1d his2sub ffvelrn adantll adantlr
      pm3.27 syl3anc eqtrd hmopf anim12i sylan breq2d cr wb subge0 hmopre
      sylanc bitrd ralbidva ) BDEZCDEZFZBCGHIAJZCBKLMZVHNLZOHZAPQVHBMZVHNLZVHCM
      ZVHNLZOHZAPQABCRVGVKVPAPVGVHPEZFZVKIVOVMSLZOHZVPVRVJVSIOPPBTZPPCTZFZVQVJV
      SUAVGWCVQFZVJVNVLUBLZVHNLZVSWDVIWEVHNWAWBVQVIWEUAZWBWAVQWGVHCBUCUDUEUFVNP
      EZVLPEZVQWFVSUAWDVNVLVHUGWBVQWHWAPPVHCUHUIWAVQWIWBPPVHBUHUJWCVQUKULUMVEWA
      VFWBBUNCUNUOUPUQVOUREZVMUREZVTVPUSVRVOVMUTVFVQWJVEVHCVAUIVEVQWKVFVHBVAUJV
      BVCVDVC $.
      $( [23-Jul-2006] $)

    $( Operator ordering in terms of a positive operator.  Definition of
       operator ordering in [Retherford] p. 49. $)
    leop3 $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                0hop <_op ( U -op T ) ) ) $=
      ( vx cho wcel wa cleo wbr cc0 cv chod co cfv csp cle chil wral ch0o leop
      wb leop2 0hmop a1i hmopd ancoms sylanc c0v ho0val opreq1d hi01 eqtrd
      breq1d ralbiia syl6rbb bitrd ) ADEZBDEZFZABGHICJZBAKLZMUSNLZOHZCPQZRUTGHZ
      CABSURVDUSRMZUSNLZVAOHZCPQZVCRDEZUTDEZVDVHTURCRUTUAVIURUBUCUQUPVJBAUDUEUF
      VGVBCPUSPEZVFIVAOVKVFUGUSNLIVKVEUGUSNUSUHUIUSUJUKULUMUNUO $.
      $( [23-Jul-2006] $)

    $( Binary relation defining a positive operator.  Definition VI.1 of
       [Retherford] p. 49. $)
    leoppos $p |- ( T e. HrmOp -> ( 0hop <_op T <->
                A. x e. ~H 0 <_ ( ( T ` x ) .ih x ) ) ) $=
      ( cho wcel ch0o cleo wbr cc0 cv chod co cfv csp cle chil wral wb 0hmop
      leop mpan wf wceq hmopf hosubid1 syl fveq1d opreq1d breq2d ralbidv bitrd
      ) BCDZEBFGZHAIZBEJKZLZUMMKZNGZAOPZHUMBLZUMMKZNGZAOPECDUKULURQRAEBSTUKUQVA
      AOUKUPUTHNUKUOUSUMMUKUMUNBUKOOBUAUNBUBBUCBUDUEUFUGUHUIUJ $.
      $( [25-Jul-2006] $)

    $( The ordering relation for operators is reflexive. $)
    leoprf2 $p |- ( T : ~H --> ~H -> T <_op T ) $=
      ( vx chil wf cleo wbr chod co cho wcel cc0 cv cfv csp cle wral cvv wa wb
      leopg ax-hilex fex mpan2 sylanc ch0o hodid 0hmop syl6eqel c0v wceq
      adantr fveq1d ho0val adantl eqtrd opreq1d hi01 eqtr2d 0re leidi syl5breq
      r19.21aiva mpbir2and ) CCADZAAEFZAAGHZIJZKBLZVFMZVHNHZOFZBCPZAQJZVMVEVGVL
      RSVDBQQAATVDCQJVMUACCQAUBUCZVNUDVDVFUEIAUFZUGUHVDVKBCVDVHCJZRZKVJKOVQVJUI
      VHNHZKVQVIUIVHNVQVIVHUEMZUIVQVHVFUEVDVFUEUJVPVOUKULVPVSUIUJVDVHUMUNUOUPVP
      VRKUJVDVHUQUNURKUSUTVAVBVC $.
      $( [24-Jul-2006] $)

    $( The ordering relation for operators is reflexive. $)
    leoprf $p |- ( T e. HrmOp -> T <_op T ) $=
      ( vx cho wcel cleo wbr cc0 cv chod co cfv csp cle chil wral wa c0v ch0o
      wceq wf hmopf hodid syl adantr fveq1d ho0val adantl eqtrd opreq1d hi01
      eqtr2d 0re leidi syl5breq r19.21aiva wb leop anidms mpbird ) ACDZAAEFZGBH
      ZAAIJZKZVBLJZMFZBNOZUTVFBNUTVBNDZPZGVEGMVIVEQVBLJZGVIVDQVBLVIVDVBRKZQVIVB
      VCRUTVCRSZVHUTNNATVLAUAAUBUCUDUEVHVKQSUTVBUFUGUHUIVHVJGSUTVBUJUGUKGULUMUN
      UOUTVAVGUPBAAUQURUS $.
      $( [23-Jul-2006] $)

    $( The square of a Hermitian operator is positive. $)
    leopsq $p |- ( T e. HrmOp -> 0hop <_op ( T o. T ) ) $=
      ( vx cho wcel ch0o ccom cleo wbr cc0 cv cfv csp co cle chil wral wa wf
      ffvelrn hmopf sylan hiidge0 syl wceq hmop pm3.26 pm3.27 syl3anc wfun
      fvco3 ffun adantr opreq1d eqtr4d breqtrd r19.21aiva wb eqid hmopco
      mp3an3 anidms leoppos mpbird ) ACDZEAAFZGHZIBJZVEKZVGLMZNHZBOPZVDVJBOVDVG
      ODZQZIVGAKZVNLMZVINVMVNODZIVONHOOARZVLVPVDOOVGASATZUAZVNUBUCVMVOVNAKZVGLM
      ZVIVDVPVLVOWAUDVMVNVGAUEVDVLUFVSVDVLUGZUHVMVHVTVGLAUIZVQVLVHVTUDVMOOVGAAU
      JVDWCVLVDVQWCVROOAUKUCULVDVQVLVRULWBUHUMUNUOUPVDVECDZVFVKUQVDWDVDVDVEVEUD
      WDVEURAAUSUTVABVEVBUCVC $.
      $( [23-Aug-2006] $)

    $( The zero operator is a positive operator.  (The literature calls it
       "positive," even though in some sense it is really "nonnegative.") Part
       of Example 12.2(i) in [Young] p. 142. $)
    0leop $p |- 0hop <_op 0hop $=
      ( ch0o cho wcel cleo wbr 0hmop leoprf ax-mp ) ABCAADEFAGH $.
      $( [23-Jul-2006] $)

    $( The identity operator is a positive operator.  Part of Example 12.2(i)
       in [Young] p. 142. $)
    idleop $p |- 0hop <_op Iop $=
      ( vx ch0o chio cleo wbr cv cfv csp co cle chil cho wcel wral wb 0hmop
      idhmop leop2 mp2an cc0 hiidge0 c0v ho0val opreq1d hi01 eqtrd hoival
      3brtr4d mprgbir ) BCDEZAFZBGZUKHIZUKCGZUKHIZJEZAKBLMCLMUJUPAKNOPQABCRSUKK
      MZTUKUKHIUMUOJUKUAUQUMUBUKHITUQULUBUKHUKUCUDUKUEUFUQUNUKUKHUKUGUDUHUI $.
      $( [23-Jul-2006] $)

    $( The sum of two positive operators is positive.  Exercise 1(i) of
       [Retherford] p. 49. $)
    leopadd $p |- ( ( ( T e. HrmOp /\ U e. HrmOp ) /\ ( 0hop <_op T
               /\ 0hop <_op U ) ) -> 0hop <_op ( T +op U ) ) $=
      ( vx cho wcel wa ch0o cleo wbr chos co cc0 cv cfv csp cle chil wral
      caddc wi cr addge0 ex hmopre syl2an anandirs wf wceq cva hosvalOLD
      opreq1d ax-his2 ffvelrn adantlr adantll pm3.27 syl3anc eqtrd hmopf
      anim12i sylan breq2d sylibrd ralimdvaa r19.26 syl5ibr leoppos bi2anan9
      wb hmops syl 3imtr4d imp ) ADEZBDEZFZGAHIZGBHIZFZGABJKZHIZVPLCMZANZWBOKZP
      IZCQRZLWBBNZWBOKZPIZCQRZFZLWBVTNZWBOKZPIZCQRZVSWAVPWEWIFZCQRWOWKVPWPWNCQV
      PWBQEZFZWPLWDWHSKZPIZWNVNVOWQWPWTTZWDUAEZWHUAEZXAVNWQFVOWQFXBXCFWPWTWDWHU
      BUCWBAUDWBBUDUEUFWRWMWSLPQQAUGZQQBUGZFZWQWMWSUHVPXFWQFZWMWCWGUIKZWBOKZWSX
      GWLXHWBOWBABUJUKWCQEZWGQEZWQXIWSUHXGWCWGWBULXDWQXJXEQQWBAUMUNXEWQXKXDQQWB
      BUMUOXFWQUPUQURVNXDVOXEAUSBUSUTVAVBVCVDWEWICQVEVFVNVQWFVOVRWJCAVGCBVGVHVP
      VTDEWAWOVIABVJCVTVGVKVLVM $.
      $( [25-Jul-2006] $)

    $( The scalar product of a nonnegative real and a positive operator is a
       positive operator.  Exercise 1(ii) of [Retherford] p. 49. $)
    leopmuli $p |- ( ( ( A e. RR /\ T e. HrmOp ) /\ ( 0 <_ A /\
                0hop <_op T ) ) -> 0hop <_op ( A .op T ) ) $=
      ( vx cr wcel cho wa cc0 cle wbr ch0o cleo chot co cv cfv csp chil wral
      cmul wi mulge0OLD simpll hmopre adantll jca sylan expr an1rs wb cc wf
      wceq csm homval 3expa opreq1d ax-his3 ffvelrn pm3.27 syl3anc eqtrd recn
      hmopf anim12i breq2d adantlr sylibrd ralimdvaa expimpd leoppos adantl
      anbi2d hmopm syl 3imtr4d imp ) ADEZBFEZGZHAIJZKBLJZGZKABMNZLJZVTWAHCOZBPZ
      WFQNZIJZCRSZGHWFWDPZWFQNZIJZCRSZWCWEVTWAWJWNVTWAGZWIWMCRWOWFREZGWIHAWHTNZ
      IJZWMVTWPWAWIWRUAVTWPGZWAWIWRVRWHDEZGWAWIGWRWSAWHUBWSVRWTVRVSWPUCVSWPWTVR
      WFBUDUEUFUGUHUIVTWPWMWRUJWAWSWLWQHIAUKEZRRBULZGZWPWLWQUMVTXCWPGZWLAWGUNNZ
      WFQNZWQXDWKXEWFQXAXBWPWKXEUMAWFBUOUPUQXAWGREZWPXFWQUMXDAWGWFURXAXBWPUCXBW
      PXGXARRWFBUSUEXCWPUTVAVBVRXAVSXBAVCBVDVEUGVFVGVHVIVJVTWBWJWAVSWBWJUJVRCBV
      KVLVMVTWDFEWEWNUJABVNCWDVKVOVPVQ $.
      $( [25-Jul-2006] $)

    $( The scalar product of a positive real and a positive operator is a
       positive operator.  Exercise 1(ii) of [Retherford] p. 49. $)
    leopmul $p |- ( ( A e. RR /\ T e. HrmOp /\ 0 < A ) ->
                ( 0hop <_op T <-> 0hop <_op ( A .op T ) ) ) $=
      ( cr wcel cho cc0 clt wbr w3a ch0o cleo chot co wa cle leopmuli 3simpa
      adantr 0re ltle 3impia mp3an1 3adant2 anim1i sylanc c1 cdiv anassrs wne
      gt0ne0 rereccl syldan hmopm 3adant3 jca recgt0 wi a1i mpd sylan wceq
      cmul cc recid2 recn opreq1d chil wf homulass reccl 3ad2ant1 hmopf
      3ad2ant2 syl3anc homulid2 syl 3eqtr3d breqtrd impbida ) ACDZBEDZFAGHZIZJB
      KHZJABLMZKHZVTWANZFAOHZWDNWFWCWDNABPWCWGWDVTWAWBQRWCWHWDVTWBWHWAFCDZVTWBW
      HSWIVTWBWHFATUAUBUCUDUEWCWFNJUFAUGMZWELMZBKWJCDZWEEDZNZFWJOHZNWFJWKKHZWCW
      NWOWFWPWJWEPUHWCWNWOWCWLWMVTWBWLWAVTWBAFUIZWLAUJZAUKULZUCVTWAWMWBABUMUNUO
      VTWBWOWAVTWBNZFWJGHZWOAUPWIWLXAWOUQWTFWJTWIWTSURWSUEUSUCUOUTWCWKBVAWFWCWJ
      AVBMZBLMZUFBLMZWKBVTWBXCXDVAWAWTXBUFBLAVCDZWQXBUFVAWTAVDVTXEWBAVEZRZWRUEV
      FUCWJVCDZXEVGVGBVHZXCWKVAWCWJABVIVTWBXHWAXEWQXHWTAVJXGWRUEUCVTWAXEWBXFVKW
      AVTXIWBBVLZVMVNWAVTXDBVAZWBWAXIXKXJBVOVPVMVQRVRVS $.
      $( [23-Aug-2006] $)

    $( Scalar product applied to operator ordering. $)
    leopmul2i $p |- ( ( ( A e. RR /\ T e. HrmOp /\ U e. HrmOp ) /\ ( 0 <_ A /\
                T <_op U ) ) -> ( A .op T ) <_op ( A .op U ) ) $=
      ( cr wcel cho w3a cc0 cle wbr cleo chot co wa ch0o chod wi leopmuli
      exp32 3simp1 hmopd ancoms 3adant1 sylanc imp wb leop3 adantr hmopm
      syl2an 3impdi wceq cc chil wf hosubdi recn hmopf syl3an 3com23 breq2d
      bitr4d 3imtr4d impr ) ADEZBFEZCFEZGZHAIJZBCKJZABLMZACLMZKJZVHVINOCBPMZKJZ
      OAVNLMZKJZVJVMVHVIVOVQQZVEVNFEZVIVRQVHVEVSNVIVOVQAVNRSVEVFVGTVFVGVSVEVGVF
      VSCBUAUBUCUDUEVHVJVOUFZVIVFVGVTVEBCUGUCUHVHVMVQUFVIVHVMOVLVKPMZKJZVQVEVFV
      GVMWBUFZVKFEVLFEWCVEVFNVEVGNVKVLUGABUIACUIUJUKVHVPWAOKVEVGVFVPWAULZAUMEUN
      UNCUOUNUNBUOWDVEVGVFACBUPAUQCURBURUSUTVAVBUHVCVD $.
      $( [12-Aug-2006] $)

    $( The positive operator ordering relation satisfies trichotomy.  Exercise
       1(iii) of [Retherford] p. 49. $)
    leoptri $p |- ( ( T e. HrmOp /\ U e. HrmOp ) ->
           ( ( T <_op U /\ U <_op T ) <-> T = U ) ) $=
      ( vx cho wcel wa cleo wbr cv cfv csp co cle chil wral wceq leop2 wb
      ancoms anbi12d cr letri3 hmopre adantlr adantll sylanc ralbidva r19.26
      syl6rbb clo lnopeq hmoplin syl2an 3bitrd ) ADEZBDEZFZABGHZBAGHZFCIZAJUTKL
      ZUTBJUTKLZMHZCNOZVBVAMHZCNOZFZVAVBPZCNOZABPZUQURVDUSVFCABQUPUOUSVFRCBAQST
      UQVIVCVEFZCNOVGUQVHVKCNVAUAEZVBUAEZVHVKRUQUTNEZFVAVBUBUOVNVLUPUTAUCUDUPVN
      VMUOUTBUCUEUFUGVCVECNUHUIAUJEBUJEVIVJRUOUPCABUKAULBULUMUN $.
      $( [23-Apr-2008] $) $( [25-Jul-2006] $)

    $( The positive operator ordering relation is transitive.  Exercise 1(iv)
       of [Retherford] p. 49. $)
    leoptr $p |- ( ( ( S e. HrmOp /\ T e. HrmOp /\ U e. HrmOp ) /\
           ( S <_op T /\ T <_op U ) ) -> S <_op U ) $=
      ( vx cho wcel w3a cleo wbr wa cv cfv csp co cle chil wral wi cr letr
      hmopre syl3an 3anandirs ralimdvaa r19.26 syl5ibr wb leop2 3adant3
      3adant1 anbi12d 3adant2 3imtr4d imp ) AEFZBEFZCEFZGZABHIZBCHIZJZACHIZURDK
      ZALVCMNZVCBLVCMNZOIZDPQZVEVCCLVCMNZOIZDPQZJZVDVHOIZDPQZVAVBURVFVIJZDPQVMV
      KURVNVLDPUOUPUQVCPFZVNVLRZVDSFVESFVHSFVPUOVOJUPVOJUQVOJVDVEVHTVCAUAVCBUAV
      CCUAUBUCUDVFVIDPUEUFURUSVGUTVJUOUPUSVGUGUQDABUHUIUPUQUTVJUGUODBCUHUJUKUOU
      QVBVMUGUPDACUHULUMUN $.
      $( [5-Jul-2008] $) $( [25-Jul-2006] $)

    $( A bounded Hermitian operator is less than or equal to its norm times the
       identity operator. $)
    leopnmid $p |- ( ( T e. HrmOp /\ ( normop ` T ) e. RR ) ->
                    T <_op ( ( normop ` T ) .op Iop ) ) $=
      ( vx cho wcel cnop cfv cr wa chio chot co cleo wbr cv csp cle chil wral
      cabs hmopre adantlr cc recnd abscl syl idhmop hmopm mpan2 sylan adantll
      leabs cno cmul remulcl wf ffvelrn normcl hmopf adantl sylanc bcs
      sylancom cc0 pm3.27 syl2an normge0 cbo nmbdoplb clo elbdop2 biimpri
      hmoplin lemul1aOLD syl32anc csm wceq c2 cexp sqval normsq eqtr3d opreq2d
      mulass recn ad2antlr syl3anc ax-his3 3eqtr4d homval wf1o hoif f1of ax-mp
      a1i hoival eqtrd opreq1d eqtr4d breqtrd letrd r19.21aiva wb leop2 sylan2
      mpbird ) ACDZAEFZGDZHZAXGIJKZLMZBNZAFZXLOKZXLXJFZXLOKZPMZBQRZXIXQBQXIXLQD
      ZHZXNXNSFZXPXFXSXNGDZXHXLATZUAXFXSYAGDZXHXFXSHZXNUBDYDYEXNYCUCXNUDUEUAZXH
      XSXPGDZXFXJCDZXSYGXHXLXJTXHICDYHUFXGIUGUHZUIUJZXFXSXNYAPMZXHYEYBYKYCXNUKU
      EUAXTYAXMULFZXLULFZUMKZXPYFYLGDZYMGDZYNGDXTYLYMUNXFXSYOXHQQAUOZXSYOXFYQXS
      HXMQDZYOQQXLAUPZXMUQUEAURZUIUAZXSYPXIXLUQZUSZUTYJXFXSYAYNPMZXHXFXSYRUUDYQ
      XSYRXFYSYTUIXMXLVAVBUAXTYNXGYMUMKZYMUMKZXPPXTYOUUEGDZYPVCYMPMZYLUUEPMZYNU
      UFPMUUAXHYPUUGXIXSXGYMUNXFXHVDUUBVEUUCXSUUHXIXLVFUSAVGDZXSUUIXIXLAVHAVIDZ
      XHUUJXFUUJUUKXHHAVJVKAVLUIUIYLUUEYMVMVNXTUUFXGXLVOKZXLOKZXPXTXGYMYMUMKZUM
      KZXGXLXLOKZUMKZUUFUUMXSUUOUUQVPXIXSUUNUUPXGUMXSYMVQVRKZUUNUUPXSYMUBDZUURU
      UNVPXSYMUUBUCYMVSUEXLVTWAWBUSXGUBDZUUSUUSUUFUUOVPXTXGYMYMWCXHUUTXFXSXGWDW
      EZXTYMUUCUCZUVBWFUUTXSXSUUMUUQVPXTXGXLXLWGUVAXIXSVDZUVCWFWHXTXOUULXLOXTXO
      XGXLIFZVOKZUULUUTQQIUOZXSXOUVEVPXTXGXLIWIUVAUVFXTQQIWJUVFWKQQIWLWMWNUVCWF
      XSUVEUULVPXIXSUVDXLXGVOXLWOWBUSWPWQWRWSWTWTXAXFYHXKXRXBXHBAXJXCYIXDXE $.
      $( [11-Aug-2006] $)

    $( A nonzero, bounded Hermitian operator divided by its norm is less than
       or equal to the identity operator. $)
    nmopleid $p |- ( ( T e. HrmOp /\ ( normop ` T ) e. RR /\ T =/= 0hop ) ->
                    ( ( 1 / ( normop ` T ) ) .op T ) <_op Iop ) $=
      ( cho wcel cnop cfv cr ch0o wne c1 cdiv co chot chio cleo wbr wa cc0 clo
      nmlnopne0 biimpar hmoplin sylan adantlr cle rereccl adantll simpll
      idhmop hmopm mpan2 ad2antlr clt recgt0 simplr chil wf nmopgt0 biimpa
      hmopf sylanc wi 0re ltle mpan syl mpd leopnmid adantr leopmul2i syl32anc
      wceq cc cmul wf1o hoif f1of ax-mp homulass mp3an3 reccl pm3.26 recid2
      opreq1d eqtr3d homulid2 syl6eq recn breqtrd syldan 3impa ) ABCZADEZFCZAGH
      ZIWLJKZALKZMNOZWKWMPZWNWLQHZWQWKWNWSWMARCZWNWSWKWTWSWNASTAUAUBUCWRWSPZWPW
      OWLMLKZLKZMNXAWOFCZWKXBBCZQWOUDOZAXBNOZWPXCNOWMWSXDWKWLUEZUFWKWMWSUGWMXEW
      KWSWMMBCXEUHWLMUIUJUKXAQWOULOZXFWMQWLULOZXIXAWLUMWKWMWSUNWKWSXJWMUOUOAUPZ
      WSXJWKXKWSXJAUQURAUSUBUCUTWMWSXIXFVAZWKWMWSPXDXLXHQFCXDXLVBQWOVCVDVEUFVFW
      RXGWSAVGVHWOAXBVIVJWMWSXCMVKZWKWLVLCZWSXMWMXNWSPZXCIMLKZMXOWOWLVMKZMLKZXC
      XPWOVLCZXNXRXCVKZXOXSXNUOUOMUPZXTUOUOMVNYAVOUOUOMVPVQZWOWLMVRVSWLVTXNWSWA
      UTXOXQIMLWLWBWCWDYAXPMVKYBMWEVQWFWLWGUBUFWHWIWJ $.
      $( [12-Aug-2006] $)
  $}

  ${
    $d u v T $.
    opsqrlem1.1 $e |- T e. HrmOp $.
    opsqrlem1.2 $e |- ( normop ` T ) e. RR $.
    opsqrlem1.3 $e |- 0hop <_op T $.
    opsqrlem1.4 $e |- R = ( ( 1 / ( normop ` T ) ) .op T ) $.
    opsqrlem1.5 $e |- ( T =/= 0hop -> E. u e. HrmOp ( 0hop <_op u /\
                      ( u o. u ) = R ) ) $.
    $( Lemma for opsqri . $)
    opsqrlem1 $p |- ( T =/= 0hop ->
                   E. v e. HrmOp ( 0hop <_op v /\ ( v o. v ) = T ) ) $=
      ( ch0o wne cv cleo wbr ccom wceq wa cho wrex wcel cnop cfv csqr chot co
      cr cc0 cle chil wf hmopf ax-mp nmopge0 sqrcli hmopm mpan ad2antlr
      sqrge0i leopmuli mpanr1 mpanl1 ad2ant2lr cc recni homco1 mp3an1 homulcl
      syl sylanc clo homco2 hmoplin opreq2d cmul fco homulass mp3an12 sqrthi
      opreq1i syl5reqr 3eqtrd c1 cdiv id syl6eq wb nmlnopne0 recidzi sylbir
      opreq1d rerecclzi recnd mp3an13 homulid2 a1i 3eqtr3d sylan9eqr adantlr
      eqtrd adantrl breq2 coeq1 coeq2 eqeq1d anbi12d rcla4ev syl12anc exp31
      r19.23adv mpd ) DJKZJBLZMNZXLXLOZCPZQZBRSJALZMNZXQXQOZDPZQZARSZIXKXPYBBRX
      KXLRTZXPYBXKYCQZXPQDUAUBZUCUBZXLUDUEZRTZJYGMNZYGYGOZDPZYBYCYHXKXPYFUFTZYC
      YHUGYEUHNZYLUIUIDUJZYMDRTZYNEDUKULZDUMULZYEFUNULZYFXLUOUPUQYCXMYIXKXOYLYC
      XMYIYRYLYCQUGYFUHNZXMYIYMYSYQYEFURULYFXLUSUTVAVBYDXOYKXMYDXOQYJYEXNUDUEZD
      YCYJYTPXKXOYCYJYFXLYGOZUDUEZYFYFXNUDUEZUDUEZYTUIUIXLUJZUIUIYGUJZYJUUBPZYC
      YFVCTZUUEUUFUUGYFYRVDZYFXLYGVEVFXLUKZYCUUEUUFUUJUUHUUEUUFUUIYFXLVGUPVHVIY
      CUUAUUCYFUDXLVJTZUUEUUAUUCPZYCUUHUUKUUEUULUUIYFXLXLVKVFXLVLUUJVIVMYCYFYFV
      NUEZXNUDUEZUUDYTYCUIUIXNUJZUUNUUDPZUUEUUEUUOYCUIUIUIXLXLVOUUJUUJVIUUHUUHU
      UOUUPUUIUUIYFYFXNVPVQVHUUMYEXNUDYMUUMYEPYQYEFVRULVSVTWAUQXKXOYTDPYCXOXKYT
      YEWBYEWCUEZDUDUEZUDUEZDXOXNUURYEUDXOXNCUURXOWDHWEVMXKYEUUQVNUEZDUDUEZWBDU
      DUEZUUSDXKUUTWBDUDXKYEUGKZUUTWBPDVJTZUVCXKWFYOUVDEDVLULDWGULZYEYEFVDZWHWI
      WJXKUUQVCTZUVAUUSPZXKUUQXKUVCUUQUFTUVEYEFWKWIWLYEVCTUVGYNUVHUVFYPYEUUQDVP
      WMVHUVBDPZXKYNUVIYPDWNULWOWPWQWRWSWTYAYIYKQAYGRXQYGPZXRYIXTYKXQYGJMXAUVJX
      SYJDUVJXSYGXQOYJXQYGXQXBXQYGYGXCWSXDXEXFXGXHXIXJ $.
      $( [28-Jul-2011] $) $( [9-Aug-2006] $)
  $}

  ${
    $d v w G $.  $d v H $.  $d v w S $.  $d v w x y z T $.  $d j k F $.
    $d j k N $.
    opsqrlem2.1 $e |- T e. HrmOp $.
    opsqrlem2.2 $e |- S = { <. <. x , y >. , z >. | ( ( x e. HrmOp /\
    y e. HrmOp ) /\ z = ( x +op ( ( 1 / 2 ) .op ( T -op ( x o. x ) ) ) ) ) } $.
    opsqrlem2.3 $e |- F = ( S seq1 ( NN X. { 0hop } ) ) $.
    $( Lemma for opsqri . ` F `` N ` is the recursive function A_n (starting at
       n=1 instead of 0) of Theorem 9.4-2 of [Kreyszig] p. 476. $)
    opsqrlem2 $p |- ( F ` 1 ) = 0hop $=
      ( c1 cfv cn ch0o csn cxp cseq1 co fveq1i cho cv c2 cdiv ccom chod chot
      chos hmopex oprabex2 nnex snex xpex seq11 wcel wceq 1nn 0hmop elisseti
      fvconst2 ax-mp 3eqtri ) JFKJDLMNZOZPQZKJVBKZMJFVCIRDVBABCSSATZJUAUBQEVEVE
      UCUDQUEQUFQDUGUGHUHLVAUIMUJUKULJLUMVDMUNUOLMJMSUPUQURUSUT $.
      $( [28-Jul-2011] $) $( [17-Aug-2006] $)

    $( Lemma for opsqri . $)
    opsqrlem3 $p |- F : NN --> HrmOp $=
      ( cn cho wf ch0o csn cxp cseq1 co wss 0hmop elisseti fconst wcel snssi
      ax-mp fss mp2an cv c1 c2 cdiv ccom chod chot chos wceq eqid hmopco
      mp3an3 anidms hmopd mpan cr 2re 2ne0 rereccli hmopm 3syl hmops mpdan
      adantr foprab hmopex oprabex2 nnex snex xpex seq1f feq1i mpbir ) JKFLJKDJ
      MNZOZPQZLZJKWALZKKOKDLWCJVTWALVTKRZWDJMMKSTUAMKUBWESMKUCUDJVTKWAUEUFABCKK
      AUGZUHUIUJQZEWFWFUKZULQZUMQZUNQZKDHWFKUBZWKKUBZBUGKUBWLWJKUBZWMWLWHKUBZWI
      KUBZWNWLWOWLWLWHWHUOWOWHUPWFWFUQURUSEKUBWOWPGEWHUTVAWGVBUBWPWNUIVCVDVEWGW
      IVFVAVGWFWJVHVIVJVKKDWAABCKKWKDVLVLHVMJVTVNMVOVPVQUFJKFWBIVRVS $.
      $( [28-Jul-2011] $) $( [17-Aug-2006] $)

    $( Lemma for opsqri . $)
    opsqrlem4 $p |- ( ( G e. HrmOp /\ H e. HrmOp ) ->
              ( G S H ) = ( G +op ( ( 1 / 2 ) .op ( T -op ( G o. G ) ) ) ) ) $=
      ( vw vv cv co c1 c2 cdiv ccom chod chot chos wceq cho opreq1 id coeq1
      coeq2 eqtrd opreq2d opreq12d eqeq12d opreq2 eqeq1d oprex eqidd oprabval2
      vtocl2ga ) LNZMNZDOZUSPQROZEUSUSSZTOZUAOZUBOZUCGUTDOZGVBEGGSZTOZUAOZUBOZU
      CGHDOZVKUCLMGHUDUDUSGUCZVAVGVFVKUSGUTDUEVMUSGVEVJUBVMUFVMVDVIVBUAVMVCVHET
      VMVCGUSSVHUSGUSUGUSGGUHUIUJUJUKULUTHUCVGVLVKUTHGDUMUNABCUSUTUDUDANZVBEVNV
      NSZTOZUAOZUBOVFDVFUSVEUBUOVNUSUCZVNUSVQVEUBVRUFVRVPVDVBUAVRVOVCETVRVOUSVN
      SVCVNUSVNUGVNUSUSUHUIUJUJUKBNUTUCVFUPJUQUR $.
      $( [28-Jul-2011] $) $( [22-Aug-2006] $)

    $( Lemma for opsqri . $)
    opsqrlem5 $p |- ( N e. NN -> ( F ` ( N + 1 ) ) =
  ( ( F ` N ) +op ( ( 1 / 2 ) .op ( T -op ( ( F ` N ) o. ( F ` N ) ) ) ) ) ) $=
      ( cn wcel c1 caddc co cfv ch0o csn cxp cseq1 c2 cdiv ccom chod chot chos
      wceq fveq1i a1i cho cv hmopex oprabex2 nnex snex xpex seq1p1 opsqrlem4
      opsqrlem3 ffvelrni peano2nn 0hmop elisseti fvconst2 syl syl6eqel sylanc
      opreq1i syl5eqr 3eqtrd ) GKLZGMNOZFPZVLDKQRZSZTOZPZGVPPZVLVOPZDOZGFPZMUAU
      BOZEWAWAUCUDOUEOUFOZVMVQUGVKVLFVPJUHUIGDVOABCUJUJAUKZWBEWDWDUCUDOUEOUFODU
      LULIUMKVNUNQUOUPUQVKWAVSDOZWCVTWAUJLVSUJLWEWCUGVKABCDEFWAVSHIJURKUJGFABCD
      EFHIJUSUTVKVSQUJVKVLKLVSQUGGVAKQVLQUJVBVCVDVEVBVFVGWAVRVSDGFVPJUHVHVIVJ
      $.
      $( [28-Jul-2011] $) $( [17-Aug-2006] $)

    ${
      opsqrlem6.4 $e |- T <_op Iop $.
      $( Lemma for opsqri . _Warning:  The HTML proof page is 0.6 megabyte in
         size_. $)
      opsqrlem6 $p |- ( N e. NN -> ( F ` N ) <_op Iop ) $=
        ( vj vk cv cfv chio cleo wbr c1 caddc co wceq fveq2 breq1d ch0o
        opsqrlem2 idleop eqbrtri cn wcel chod c2 chot ccom chos cho wb idhmop
        leop3 mp2an mpbi wa hmopd leopadd mpanl2 mpanr2 eqid hmopco mp3an3
        opsqrlem3 ffvelrni mpan syl sylanc leopsq 3syl cdiv chil wf cc 2cn
        hoadddi mp3an1 hmopf fco ax-mp hosubcl 2ne0 reccli homulcl cmul
        homulass mp3an12 homulid2 recidi opreq1i syl5eq eqtr3d opreq2d eqtrd
        hoaddcl hosubdi ho2times a1i hoaddsubass hoadd23 mp3an13 3eqtr4rd
        opreq1d mp3an23 hosubsub4 3eqtr3d hosubadd4 mpanr1 mpanl1 3eqtr2d
        hocsubdir clo hmoplin hoddi hoid1i hoico2 opreq12d mp3an2 hoico1 jctil
        syl12anc 3eqtrd opsqrlem5 3eqtr4d breqtrd peano2nn cr cc0 clt 2re 2pos
        leopmul mpbird mpan2 nn1suc ) LNZFOZPQRSFOZPQRMNZSTUAZFOZPQRZGFOZPQRLMG
        UUBSUBUUCUUDPQUUBSFUCUDUUBUUFUBUUCUUGPQUUBUUFFUCUDUUBGUBUUCUUIPQUUBGFUC
        UDUUDUEPQABCDEFHIJUFUGUHUUEUIUJZUUHUEPUUGUKUAZQRZUUJUULUEULUUKUMUAZQRZU
        UJUEPUUEFOZUKUAZUUPUNZPEUKUAZUOUAZUUMQUUQUPUJZUEUUQQRZUEUUSQRZUUJUUTUVA
        UEUURQRZUVBEPQRZUVCKEUPUJZPUPUJZUVDUVCUQHUREPUSUTVAUUTUURUPUJZUVAUVCVBU
        VBUVFUVEUVGURHPEVCUTUUQUURVDVEVFUUPUPUJZUVHUUTUUJUVHUVHUUQUUQUBUUTUUQVG
        UUPUUPVHVIUUJUUOUPUJZUVHUIUPUUEFABCDEFHIJVJZVKZUVFUVIUVHURPUUOVCVLZVMZU
        VMVNUUJUVIUVHUVAUVKUVLUUPVOVPVNUUJPUUOUUOUNZULUUOUMUAZUKUAZUOUAZUURUOUA
        ZULPUUOSULVQUAZEUVNUKUAZUMUAZUOUAZUKUAZUMUAZUUSUUMUUJULPUMUAZULUWBUMUAZ
        UKUAZUWEUVOUVTUOUAZUKUAZUWDUVRUUJUWFUWHUWEUKUUJUWFUVOULUWAUMUAZUOUAZUWH
        VRVRUUOVSZVRVRUWAVSZUWFUWKUBZUUJULVTUJZUWLUWMUWNWAULUUOUWAWBWCUUJUVIUWL
        UVKUUOWDZVMZUUJVRVRUVNVSZVRVRUVTVSZUWMUWLUWLUWRUUJVRVRVRUUOUUOWEUWQUWQV
        NZVRVREVSZUWRUWSUVEUXAHEWDWFZEUVNWGVLZUVSVTUJZUWSUWMULWAWHWIZUVSUVTWJVL
        VPZVNUUJUWJUVTUVOUOUUJULUVSWKUAZUVTUMUAZUWJUVTUUJUWRUWSUXHUWJUBZUWTUXCU
        WOUXDUWSUXIWAUXEULUVSUVTWLWMVPUUJSUVTUMUAZUVTUXHUUJUWRUWSUXJUVTUBUWTUXC
        UVTWNVPUXGSUVTUMULWAWHWOWPWQWRWSWTWSUUJVRVRUWBVSZUWDUWGUBZUWLUWMUXKUUJU
        UOUWAXAUWQUXFVNUWOVRVRPVSZUXKUXLWAUVFUXMURPWDWFZULPUWBXBWMVMUUJUVRUWEUV
        NUOUAZUVOEUOUAUKUAZUWEUVOUKUAUVTUKUAZUWIUUJUVQPUOUAZEUKUAZUXOUVOUKUAZEU
        KUAZUVRUXPUUJUXRUXTEUKUUJUWEUVPUOUAZPPUOUAZUVPUOUAZUXTUXRUYBUYDUBUUJUWE
        UYCUVPUOUXMUWEUYCUBUXNPXCWFWPXDUWRVRVRUVOVSZUXTUYBUBZUUJVRVRUWEVSZUWRUY
        EUYFUWOUXMUYGWAUXNULPWJUTZUWEUVNUVOXEWCUWTUUJUVIUWLUYEUVKUWPUWOUWLUYEWA
        ULUUOWJVLVPZVNUUJVRVRUVPVSZUXRUYDUBZUWRUYEUYJUUJUVNUVOWGUWTUYIVNZUXMUYJ
        UXMUYKUXNUXNPUVPPXFXGVMXHXIUUJUYJVRVRUVQVSZUXSUVRUBZUYLUXMUYJUYMUXNPUVP
        XAVLUYMUXMUXAUYNUXNUXBUVQPEXEXJVPVRVRUXOVSZUYEUYAUXPUBZUUJUYOUYEUXAUYPU
        XBUXOUVOEXKVIUUJUWRUYOUWTUYGUWRUYOUYHUWEUVNXAVLVMUYIVNXLUYEUWRUXQUXPUBZ
        UUJUYGUYEUWRUYQUYHUYGUYEVBUXAUWRUYQUXBUWEUVOEUVNXMXNXOUYIUWTVNUYEUWSUXQ
        UWIUBZUUJUYGUYEUWSUYRUYHUWEUVOUVTXKWCUYIUUJUWRUWSUWTUXCVMVNXPXHUUJUUQUV
        QUURUOUUJUUQPUUPUNZUUOUUPUNZUKUAZPUVNUOUAZUUOUUOUOUAZUKUAZUVQUWLVRVRUUP
        VSZUUQVUAUBZUUJUXMUWLVUEVUFUXNPUUOUUPXQWCUWQUUJUVIUWLVUEUVKUWPUXMUWLVUE
        UXNPUUOWGVLVPVNUUJVUAUUPUUOUVNUKUAZUKUAZVUDUUJUYSUUPUYTVUGUKUUJUYSPPUNZ
        PUUOUNZUKUAZUUPUUJUVIUWLUYSVUKUBZUVKUWPPXRUJZUXMUWLVULUVFVUMURPXSWFUXNP
        PUUOXTWMVPUUJVUIPVUJUUOUKVUIPUBUUJPUXNYAXDUUJUVIUWLVUJUUOUBUVKUWPUUOYBV
        PYCWTUUJUYTUUOPUNZUVNUKUAZVUGUUOXRUJZUWLUYTVUOUBZUUJVUPUXMUWLVUQUXNUUOP
        UUOXTYDUUJUVIVUPUVKUUOXSVMUWQVNUUJVUNUUOUVNUKUUJUVIUWLVUNUUOUBUVKUWPUUO
        YEVPXIWTYCUUJUXMUWLVBUWLUWRVUHVUDUBUUJUWLUXMUWQUXNYFUWQUWTPUUOUUOUVNXMY
        GWTUUJVUBUVOUKUAZVUDUVQUUJUVOVUCVUBUKUUJUVIUWLUVOVUCUBUVKUWPUUOXCVPWSUW
        RUYEVURUVQUBZUUJUXMUWRUYEVUSUXNPUVNUVOXEWCUWTUYIVNWRYHXIUUJUUKUWCULUMUU
        JUUGUWBPUKABCDEFUUEHIJYIWSWSYJYKUUJUUGUPUJZUUKUPUJZUULUUNUQZUUJUUFUIUJZ
        VUTUUEYLZUIUPUUFFUVJVKZVMUVFVUTVVAURPUUGVCVLULYMUJVVAYNULYORVVBYPYQULUU
        KYRXGVPYSUUJVVCVUTUUHUULUQZVVDVVEVUTUVFVVFURUUGPUSYTVPYSUUA $.
        $( [28-Jul-2011] $) $( [23-Aug-2006] $)
    $}
  $}

$(
  @{
    opsqrlem8.1 @e |- T e. HrmOp @.
    opsqrlem8.2 @e |- 0hop <_op T @.
    opsqrlem8.3 @e |- T <_op Iop @.
    @( Lemma for ~ opsqri . @)
    opsqrlem8 @p |- E. u e. HrmOp ( 0hop <_op u /\
                    ( u o. u ) = T ) @=
      (  ) ? @.
      @( [ ?] @) @( [17-Aug-2006] @)
  @}

  @{
    @d u T @.
    @( Lemma for ~ opsqri . @)
    opsqrlem9 @p |- ( ( T e. HrmOp /\ 0hop <_op T /\ T <_op Iop ) ->
                 E. u e. HrmOp ( 0hop <_op u /\ ( u o. u ) = T ) ) @=
      ( cho wcel ch0o cleo wbr chio w3a cv ccom wceq wa wrex cif eqeq2 anbi2d
      rexbidv eleq1 breq2 breq1 3anbi123d 0hmop 0leop idleop 3pm3.2i elimhyp
      3simp1i 3simp2i 3simp3i opsqrlem8 dedth ) BCDZEBFGZBHFGZIZEAJZFGZUQUQKZBL
      ZMZACNURUSUPBEOZLZMZACNBEBVBLZVAVDACVEUTVCURBVBUSPQRAVBVBCDZEVBFGZVBHFGZU
      PVFVGVHIECDZEEFGZEHFGZIBEVEUMVFUNVGUOVHBVBCSBVBEFTBVBHFUAUBEVBLVIVFVJVGVK
      VHEVBCSEVBEFTEVBHFUAUBVIVJVKUCUDUEUFUGZUHVFVGVHVLUIVFVGVHVLUJUKUL @.
      @( [ ?] @) @( [8-Aug-2006] @)
  @}

  @{
    @d u v T @.
    opsqr.1 @e |- T e. HrmOp @.
    opsqr.2 @e |- ( normop ` T ) e. RR @.
    opsqr.3 @e |- 0hop <_op T @.
    @( Square root lemma. @)
    opsqri @p |- E. u e. HrmOp ( 0hop <_op u /\
                    ( u o. u ) = T ) @=
      ( vv ch0o wceq cv cleo wbr ccom wa cho wrex eqid eqeq2 anbi2d rexbidv
      wne c1 cnop cfv cdiv co chot wcel chio opsqrlem9 cr cc0 clo hmoplin
      ax-mp nmlnop0iHIL biimpi necon3i rerecclzi syl hmopm mpan2 cle leopmuli
      mpanr2 mpanl2 clt wi 0re ltle mpan nmlnopgt0i recgt0i sylbi sylc sylanc
      nmopleid mp3an12 syl3anc opsqrlem1 adantl 0hmop 0leop ho0f ho0coi pm3.2i
      breq2 coeq1 coeq2 eqtrd eqeq1d anbi12d rcla4ev mp2an a1i pm2.61ne ) GGHZG
      AIZJKZWQWQLZBHZMZANOZGPWPXBWRWSGHZMZANOZBGBGHZXAXDANXFWTXCWRBGWSQRSBGTZXB
      WPAFUABUBUCZUDUEZBUFUEZBCDEXJPXJNUGZGXJJKZXJUHJKZGFIZJKXNXNLXJHMFNOXGFXJU
      IXGXIUJUGZXKXGXHUKTXOXHUKBGXHUKHXFBBNUGZBULUGCBUMUNZUOUPUQXHDURUSZXOXPXKC
      XIBUTVAUSXOUKXIVBKZXLXGXOXPXSXLCXOXPMXSGBJKXLEXIBVCVDVEXRXOUKXIVFKZXSXGUK
      UJUGXOXTXSVGVHUKXIVIVJXRXGUKXHVFKXTBXQVKXHDVLVMVNVOXPXHUJUGXGXMCDBVPVQVRV
      SVTXEWPGNUGGGJKZGGLZGHZMZXEWAYAYCWBGWCWDWEXDYDAGNWQGHZWRYAXCYCWQGGJWFYEWS
      YBGYEWSGWQLYBWQGWQWGWQGGWHWIWJWKWLWMWNWOUN @.
      @( [ ?] @) @( [8-Aug-2006] @)
  @}
$)

