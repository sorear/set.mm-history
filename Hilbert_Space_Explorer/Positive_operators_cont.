$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Positive operators (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

    $( Ordering relation for positive operators.  Definition of positive
       operator ordering in [Kreyszig] p. 470. $)
    leopg $p |- ( ( T e. A /\ U e. B ) -> ( T <_op U <-> ( ( U -op T ) e.
                HrmOp /\ A. x e. ~H 0 <_ ( ( ( U -op T ) ` x ) .ih x ) ) ) ) $=
      ( vu vt cv chod co cho wcel cc0 cfv csp cle wbr chil wral wa cleo opreq1d
      wceq opreq2 eleq1d fveq1d breq2d ralbidv anbi12d opreq1 df-leop brabg ) F
      HZGHZIJZKLZMAHZUONZUQOJZPQZARSZTUMDIJZKLZMUQVBNZUQOJZPQZARSZTEDIJZKLZMUQV
      HNZUQOJZPQZARSZTGFDEBCUAUNDUCZUPVCVAVGVNUOVBKUNDUMIUDZUEVNUTVFARVNUSVEMPV
      NURVDUQOVNUQUOVBVOUFUBUGUHUIUMEUCZVCVIVGVMVPVBVHKUMEDIUJZUEVPVFVLARVPVEVK
      MPVPVDVJUQOVPUQVBVHVQUFUBUGUHUIAFGUKUL $.
      $( [23-Jul-2006] $)

    $( Ordering relation for operators.  Definition of positive operator
       ordering in [Kreyszig] p. 470. $)
    leop $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                A. x e. ~H 0 <_ ( ( ( U -op T ) ` x ) .ih x ) ) ) $=
      ( cho wcel wa cleo wbr chod co cc0 cv cfv csp cle chil leopg hmopd ancoms
      wral biantrurd bitr4d ) BDEZCDEZFZBCGHCBIJZDEZKALZUFMUHNJOHAPTZFUIADDBCQU
      EUGUIUDUCUGCBRSUAUB $.
      $( [23-Jul-2006] $)

    $( Ordering relation for operators.  Definition of operator ordering in
       [Young] p. 141. $)
    leop2 $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                A. x e. ~H ( ( T ` x ) .ih x ) <_ ( ( U ` x ) .ih x ) ) ) $=
      ( cho wcel wa wbr cc0 co cfv csp cle chil wral wceq hmopf ffvelrn adantll
      wf adantlr cleo cv chod leop cmin anim12i cmv hodval 3com12 3expa opreq1d
      simpr his2sub syl111anc eqtrd sylan breq2d cr wb hmopre syl11anc ralbidva
      subge0 bitrd ) BDEZCDEZFZBCUAGHAUBZCBUCIJZVHKIZLGZAMNVHBJZVHKIZVHCJZVHKIZ
      LGZAMNABCUDVGVKVPAMVGVHMEZFZVKHVOVMUEIZLGZVPVRVJVSHLVGMMBSZMMCSZFZVQVJVSO
      VEWAVFWBBPCPUFWCVQFZVJVNVLUGIZVHKIZVSWDVIWEVHKWAWBVQVIWEOZWBWAVQWGVHCBUHU
      IUJUKWDVNMEZVLMEZVQWFVSOWBVQWHWAMMVHCQRWAVQWIWBMMVHBQTWCVQULVNVLVHUMUNUOU
      PUQVRVOUREZVMUREZVTVPUSVFVQWJVEVHCUTRVEVQWKVFVHBUTTVOVMVCVAVDVBVD $.
      $( [23-Jul-2006] $)

    $( Operator ordering in terms of a positive operator.  Definition of
       operator ordering in [Retherford] p. 49. $)
    leop3 $p |- ( ( T e. HrmOp /\ U e. HrmOp ) -> ( T <_op U <->
                0hop <_op ( U -op T ) ) ) $=
      ( vx cho wcel wa cleo wbr cc0 cv chod cfv csp cle chil wral ch0o leop c0v
      co 0hmop a1i hmopd ancoms leop2 syl11anc ho0val opreq1d hi01 eqtrd breq1d
      wb ralbiia syl6rbb bitrd ) ADEZBDEZFZABGHICJZBAKTZLUSMTZNHZCOPZQUTGHZCABR
      URVDUSQLZUSMTZVANHZCOPZVCURQDEZUTDEZVDVHULVIURUAUBUQUPVJBAUCUDCQUTUEUFVGV
      BCOUSOEZVFIVANVKVFSUSMTIVKVESUSMUSUGUHUSUIUJUKUMUNUO $.
      $( [23-Jul-2006] $)

    $( Binary relation defining a positive operator.  Definition VI.1 of
       [Retherford] p. 49. $)
    leoppos $p |- ( T e. HrmOp -> ( 0hop <_op T <->
                A. x e. ~H 0 <_ ( ( T ` x ) .ih x ) ) ) $=
      ( cho wcel ch0o cleo wbr cc0 cv chod co cfv csp chil wral 0hmop leop mpan
      cle wb wf wceq hmopf hosubid1 syl fveq1d opreq1d breq2d ralbidv bitrd ) B
      CDZEBFGZHAIZBEJKZLZUMMKZSGZANOZHUMBLZUMMKZSGZANOECDUKULURTPAEBQRUKUQVAANU
      KUPUTHSUKUOUSUMMUKUMUNBUKNNBUAUNBUBBUCBUDUEUFUGUHUIUJ $.
      $( [25-Jul-2006] $)

    $( The ordering relation for operators is reflexive. $)
    leoprf2 $p |- ( T : ~H --> ~H -> T <_op T ) $=
      ( vx chil wf cleo wbr chod co cho wcel cc0 cfv csp cle cvv wa ch0o adantl
      c0v wceq cv wb ax-hilex fex mpan2 leopg syl11anc hodid 0hmop syl6eqel 0re
      wral leidi adantr fveq1d ho0val opreq1d hi01 syl5breq ralrimiva mpbir2and
      eqtrd eqtr2d ) CCADZAAEFZAAGHZIJZKBUAZVFLZVHMHZNFZBCULZVDAOJZVMVEVGVLPUBV
      DCOJVMUCCCOAUDUEZVNBOOAAUFUGVDVFQIAUHZUIUJVDVKBCVDVHCJZPZKKVJNKUKUMVQVJSV
      HMHZKVQVISVHMVQVIVHQLZSVQVHVFQVDVFQTVPVOUNUOVPVSSTVDVHUPRVBUQVPVRKTVDVHUR
      RVCUSUTVA $.
      $( [24-Jul-2006] $)

    $( The ordering relation for operators is reflexive. $)
    leoprf $p |- ( T e. HrmOp -> T <_op T ) $=
      ( vx cho wcel cleo wbr cc0 cv chod co cfv csp cle chil wral c0v ch0o wceq
      wa adantl 0re leidi wf hmopf hodid syl adantr fveq1d ho0val eqtrd opreq1d
      hi01 eqtr2d syl5breq ralrimiva wb leop anidms mpbird ) ACDZAAEFZGBHZAAIJZ
      KZVBLJZMFZBNOZUTVFBNUTVBNDZSZGGVEMGUAUBVIVEPVBLJZGVIVDPVBLVIVDVBQKZPVIVBV
      CQUTVCQRZVHUTNNAUCVLAUDAUEUFUGUHVHVKPRUTVBUITUJUKVHVJGRUTVBULTUMUNUOUTVAV
      GUPBAAUQURUS $.
      $( [23-Jul-2006] $)

    $( The square of a Hermitian operator is positive. $)
    leopsq $p |- ( T e. HrmOp -> 0hop <_op ( T o. T ) ) $=
      ( vx cho wcel ch0o ccom cleo wbr cc0 cv cfv csp co cle chil wral syl wceq
      syl111anc adantr wa wf hmopf ffvelrn sylan hiidge0 simpl simpr hmop fvco3
      wfun ffun opreq1d eqtr4d breqtrd ralrimiva wb hmopco mp3an3 anidms mpbird
      eqid leoppos ) ACDZEAAFZGHZIBJZVEKZVGLMZNHZBOPZVDVJBOVDVGODZUAZIVGAKZVNLM
      ZVINVMVNODZIVONHVDOOAUBZVLVPAUCZOOVGAUDUEZVNUFQVMVOVNAKZVGLMZVIVMVDVPVLVO
      WARVDVLUGVSVDVLUHZVNVGAUISVMVHVTVGLVMAUKZVQVLVHVTRVDWCVLVDVQWCVROOAULQTVD
      VQVLVRTWBOOVGAAUJSUMUNUOUPVDVECDZVFVKUQVDWDVDVDVEVERWDVEVBAAURUSUTBVEVCQV
      A $.
      $( [23-Aug-2006] $)

    $( The zero operator is a positive operator.  (The literature calls it
       "positive," even though in some sense it is really "nonnegative.") Part
       of Example 12.2(i) in [Young] p. 142. $)
    0leop $p |- 0hop <_op 0hop $=
      ( ch0o cho wcel cleo wbr 0hmop leoprf ax-mp ) ABCAADEFAGH $.
      $( [23-Jul-2006] $)

    $( The identity operator is a positive operator.  Part of Example 12.2(i)
       in [Young] p. 142. $)
    idleop $p |- 0hop <_op Iop $=
      ( vx ch0o chio cleo wbr cv cfv csp co cle chil cho wcel wral 0hmop idhmop
      wb cc0 c0v opreq1d leop2 mp2an hiidge0 ho0val hi01 hoival 3brtr4d mprgbir
      eqtrd ) BCDEZAFZBGZUKHIZUKCGZUKHIZJEZAKBLMCLMUJUPAKNQOPABCUAUBUKKMZRUKUKH
      IUMUOJUKUCUQUMSUKHIRUQULSUKHUKUDTUKUEUIUQUNUKUKHUKUFTUGUH $.
      $( [23-Jul-2006] $)

    $( The sum of two positive operators is positive.  Exercise 1(i) of
       [Retherford] p. 49. $)
    leopadd $p |- ( ( ( T e. HrmOp /\ U e. HrmOp ) /\ ( 0hop <_op T
               /\ 0hop <_op U ) ) -> 0hop <_op ( T +op U ) ) $=
      ( vx cho wcel wa ch0o cleo wbr co cc0 cfv csp chil wral cr hmopre leoppos
      cle wceq chos cv r19.26 caddc wi addge0 ex syl2an anandirs wf anim12i cva
      hmopf opreq1d 3expa ffvelrn adantlr adantll simpr ax-his2 syl111anc eqtrd
      w3a hosval sylan breq2d sylibrd ralimdva syl5bir bi2anan9 syl 3imtr4d imp
      wb hmops ) ADEZBDEZFZGAHIZGBHIZFZGABUAJZHIZVRKCUBZALZWDMJZSIZCNOZKWDBLZWD
      MJZSIZCNOZFZKWDWBLZWDMJZSIZCNOZWAWCWMWGWKFZCNOVRWQWGWKCNUCVRWRWPCNVRWDNEZ
      FZWRKWFWJUDJZSIZWPVPVQWSWRXBUEZVPWSFWFPEZWJPEZXCVQWSFWDAQWDBQXDXEFWRXBWFW
      JUFUGUHUIWTWOXAKSVRNNAUJZNNBUJZFZWSWOXATVPXFVQXGAUMBUMUKXHWSFZWOWEWIULJZW
      DMJZXAXFXGWSWOXKTXFXGWSVCWNXJWDMWDABVDUNUOXIWENEZWINEZWSXKXATXFWSXLXGNNWD
      AUPUQXGWSXMXFNNWDBUPURXHWSUSWEWIWDUTVAVBVEVFVGVHVIVPVSWHVQVTWLCARCBRVJVRW
      BDEWCWQVNABVOCWBRVKVLVM $.
      $( [25-Jul-2006] $)

    $( The scalar product of a nonnegative real and a positive operator is a
       positive operator.  Exercise 1(ii) of [Retherford] p. 49. $)
    leopmuli $p |- ( ( ( A e. RR /\ T e. HrmOp ) /\ ( 0 <_ A /\
                0hop <_op T ) ) -> 0hop <_op ( A .op T ) ) $=
      ( vx cr wcel cho wa cc0 cle wbr ch0o cleo co cfv csp chil wral wb leoppos
      wceq chot cv cmul wi hmopre mulge0 sylanr1 expr an4s anassrs recn anim12i
      cc wf hmopf csm homval 3expa opreq1d simpll ffvelrn adantll simpr ax-his3
      syl111anc eqtrd sylan breq2d adantlr sylibrd ralimdva adantl anbi2d hmopm
      expimpd syl 3imtr4d imp ) ADEZBFEZGZHAIJZKBLJZGZKABUAMZLJZWAWBHCUBZBNZWGO
      MZIJZCPQZGHWGWENZWGOMZIJZCPQZWDWFWAWBWKWOWAWBGZWJWNCPWPWGPEZGWJHAWIUCMZIJ
      ZWNWAWBWQWJWSUDZVSWBVTWQWTVSWBGZVTWQGZWJWSXBXAWIDEWJWSWGBUEAWIUFUGUHUIUJW
      AWQWNWSRWBWAWQGWMWRHIWAAUMEZPPBUNZGZWQWMWRTVSXCVTXDAUKBUOULXEWQGZWMAWHUPM
      ZWGOMZWRXFWLXGWGOXCXDWQWLXGTAWGBUQURUSXFXCWHPEZWQXHWRTXCXDWQUTXDWQXIXCPPW
      GBVAVBXEWQVCAWHWGVDVEVFVGVHVIVJVKVOWAWCWKWBVTWCWKRVSCBSVLVMWAWEFEWFWORABV
      NCWESVPVQVR $.
      $( [25-Jul-2006] $)

    $( The scalar product of a positive real and a positive operator is a
       positive operator.  Exercise 1(ii) of [Retherford] p. 49. $)
    leopmul $p |- ( ( A e. RR /\ T e. HrmOp /\ 0 < A ) ->
                ( 0hop <_op T <-> 0hop <_op ( A .op T ) ) ) $=
      ( cr wcel cho cc0 clt wbr ch0o cleo co wa cle adantr 0re 3adant2 syl11anc
      chot c1 wceq w3a 3simpa ltle 3impia mp3an1 anim1i leopmuli gt0ne0 rereccl
      cdiv wne syldan hmopm 3adant3 jca recgt0 wi sylancr anassrs sylan cmul cc
      recn recid2 opreq1d chil reccl 3ad2ant1 hmopf 3ad2ant2 homulass syl111anc
      mpd wf homulid2 syl 3eqtr3d breqtrd impbida ) ACDZBEDZFAGHZUAZIBJHZIABRKZ
      JHZWCWDLVTWALZFAMHZWDLWFWCWGWDVTWAWBUBNWCWHWDVTWBWHWAFCDZVTWBWHOWIVTWBWHF
      AUCUDUEPUFABUGQWCWFLISAUJKZWERKZBJWCWJCDZWEEDZLZFWJMHZLWFIWKJHZWCWNWOWCWL
      WMVTWBWLWAVTWBAFUKZWLAUHZAUIULZPVTWAWMWBABUMUNUOVTWBWOWAVTWBLZFWJGHZWOAUP
      WTWIWLXAWOUQOWSFWJUCURVMPUOWNWOWFWPWJWEUGUSUTWCWKBTWFWCWJAVAKZBRKZSBRKZWK
      BVTWBXCXDTWAWTXBSBRWTAVBDZWQXBSTVTXEWBAVCZNZWRAVDQVEPWCWJVBDZXEVFVFBVNZXC
      WKTVTWBXHWAWTXEWQXHXGWRAVGQPVTWAXEWBXFVHWAVTXIWBBVIZVJWJABVKVLWAVTXDBTZWB
      WAXIXKXJBVOVPVJVQNVRVS $.
      $( [23-Aug-2006] $)

    $( Scalar product applied to operator ordering. $)
    leopmul2i $p |- ( ( ( A e. RR /\ T e. HrmOp /\ U e. HrmOp ) /\ ( 0 <_ A /\
                T <_op U ) ) -> ( A .op T ) <_op ( A .op U ) ) $=
      ( wcel cho wbr cleo chot co wa ch0o chod wi 3adant1 wb leop3 adantr hmopm
      chil wf w3a cc0 cle simp1 hmopd ancoms leopmuli exp32 syl11anc imp syl2an
      cr 3impdi wceq cc recn hmopf hosubdi syl3an 3com23 breq2d bitr4d 3imtr4d
      impr ) AULDZBEDZCEDZUAZUBAUCFZBCGFZABHIZACHIZGFZVHVIJKCBLIZGFZKAVNHIZGFZV
      JVMVHVIVOVQMZVHVEVNEDZVIVRMVEVFVGUDVFVGVSVEVGVFVSCBUEUFNVEVSJVIVOVQAVNUGU
      HUIUJVHVJVOOZVIVFVGVTVEBCPNQVHVMVQOVIVHVMKVLVKLIZGFZVQVEVFVGVMWBOZVEVFJVK
      EDVLEDWCVEVGJABRACRVKVLPUKUMVHVPWAKGVEVGVFVPWAUNZVEAUODVGSSCTVFSSBTWDAUPC
      UQBUQACBURUSUTVAVBQVCVD $.
      $( [12-Aug-2006] $)

    $( The positive operator ordering relation satisfies trichotomy.  Exercise
       1(iii) of [Retherford] p. 49. $)
    leoptri $p |- ( ( T e. HrmOp /\ U e. HrmOp ) ->
           ( ( T <_op U /\ U <_op T ) <-> T = U ) ) $=
      ( vx cho wcel wa cleo wbr cfv csp co cle chil wral leop2 wb cr hmopre clo
      wceq ancoms anbi12d adantlr adantll letri3 syl11anc r19.26 syl6rbb lnopeq
      cv ralbidva hmoplin syl2an 3bitrd ) ADEZBDEZFZABGHZBAGHZFCUJZAIUTJKZUTBIU
      TJKZLHZCMNZVBVALHZCMNZFZVAVBTZCMNZABTZUQURVDUSVFCABOUPUOUSVFPCBAOUAUBUQVI
      VCVEFZCMNVGUQVHVKCMUQUTMEZFVAQEZVBQEZVHVKPUOVLVMUPUTARUCUPVLVNUOUTBRUDVAV
      BUEUFUKVCVECMUGUHUOASEBSEVIVJPUPAULBULCABUIUMUN $.
      $( [23-Apr-2008] $) $( [25-Jul-2006] $)

    $( The positive operator ordering relation is transitive.  Exercise 1(iv)
       of [Retherford] p. 49. $)
    leoptr $p |- ( ( ( S e. HrmOp /\ T e. HrmOp /\ U e. HrmOp ) /\
           ( S <_op T /\ T <_op U ) ) -> S <_op U ) $=
      ( vx cho wcel w3a cleo wbr wa cfv csp co cle chil wral cr hmopre wb leop2
      cv r19.26 wi letr syl3an ralimdva syl5bir 3adant3 3adant1 anbi12d 3adant2
      3anandirs 3imtr4d imp ) AEFZBEFZCEFZGZABHIZBCHIZJZACHIZURDUAZAKVCLMZVCBKV
      CLMZNIZDOPZVEVCCKVCLMZNIZDOPZJZVDVHNIZDOPZVAVBVKVFVIJZDOPURVMVFVIDOUBURVN
      VLDOUOUPUQVCOFZVNVLUCZUOVOJVDQFUPVOJVEQFUQVOJVHQFVPVCARVCBRVCCRVDVEVHUDUE
      ULUFUGURUSVGUTVJUOUPUSVGSUQDABTUHUPUQUTVJSUODBCTUIUJUOUQVBVMSUPDACTUKUMUN
      $.
      $( [5-Jul-2008] $) $( [25-Jul-2006] $)

    $( A bounded Hermitian operator is less than or equal to its norm times the
       identity operator. $)
    leopnmid $p |- ( ( T e. HrmOp /\ ( normop ` T ) e. RR ) ->
                    T <_op ( ( normop ` T ) .op Iop ) ) $=
      ( vx cho wcel cfv cr wa chio co wbr csp cle chil adantlr syl sylan adantl
      cc cmul wceq cnop chot cleo cv wral hmopre recnd abscl idhmop hmopm mpan2
      cabs adantll leabs cno hmopf ffvelrn normcl remulcl syl11anc bcs sylancom
      cc0 normge0 jca cbo clo hmoplin elbdop2 biimpri nmbdoplb lemul1a syl31anc
      wf sylan2 csm c2 cexp sqval normsq eqtr3d opreq2d recn ad2antlr syl111anc
      mulass simpr ax-his3 3eqtr4d wf1o hoif f1of ax-mp a1i homval hoival eqtrd
      opreq1d eqtr4d breqtrd letrd ralrimiva wb leop2 mpbird ) ACDZAUAEZFDZGZAX
      GHUBIZUCJZBUDZAEZXLKIZXLXJEZXLKIZLJZBMUEZXIXQBMXIXLMDZGZXNXNULEZXPXFXSXNF
      DZXHXLAUFZNXFXSYAFDZXHXFXSGZXNRDYDYEXNYCUGXNUHONZXHXSXPFDZXFXHXJCDZXSYGXH
      HCDYHUIXGHUJUKZXLXJUFPUMZXFXSXNYALJZXHYEYBYKYCXNUNONXTYAXMUOEZXLUOEZSIZXP
      YFXTYLFDZYMFDZYNFDXFXSYOXHXFMMAVNZXSYOAUPZYQXSGXMMDZYOMMXLAUQZXMUROPNZXSY
      PXIXLURZQZYLYMUSUTYJXFXSYAYNLJZXHXFXSYSUUDXFYQXSYSYRYTPXMXLVAVBNXTYNXGYMS
      IZYMSIZXPLXTYOUUEFDZYPVCYMLJZGZYLUUELJZYNUUFLJUUAXHXSUUGXFXSXHYPUUGUUBXGY
      MUSVOUMXSUUIXIXSYPUUHUUBXLVDVEQXIAVFDZXSUUJXFAVGDZXHUUKAVHUUKUULXHGAVIVJP
      XLAVKPYLUUEYMVLVMXTUUFXGXLVPIZXLKIZXPXTXGYMYMSIZSIZXGXLXLKIZSIZUUFUUNXSUU
      PUURTXIXSUUOUUQXGSXSYMVQVRIZUUOUUQXSYMRDZUUSUUOTXSYMUUBUGYMVSOXLVTWAWBQXT
      XGRDZUUTUUTUUFUUPTXHUVAXFXSXGWCWDZXTYMUUCUGZUVCXGYMYMWFWEXTUVAXSXSUUNUURT
      UVBXIXSWGZUVDXGXLXLWHWEWIXTXOUUMXLKXTXOXGXLHEZVPIZUUMXTUVAMMHVNZXSXOUVFTU
      VBUVGXTMMHWJUVGWKMMHWLWMWNUVDXGXLHWOWEXSUVFUUMTXIXSUVEXLXGVPXLWPWBQWQWRWS
      WTXAXAXBXHXFYHXKXRXCYIBAXJXDVOXE $.
      $( [11-Aug-2006] $)

    $( A nonzero, bounded Hermitian operator divided by its norm is less than
       or equal to the identity operator. $)
    nmopleid $p |- ( ( T e. HrmOp /\ ( normop ` T ) e. RR /\ T =/= 0hop ) ->
                    ( ( 1 / ( normop ` T ) ) .op T ) <_op Iop ) $=
      ( cho wcel cr wne c1 co chot chio cleo wbr cc0 sylan adantlr adantll chil
      wa clt wf wceq cnop cfv ch0o clo hmoplin nmlnopne0 biimpar rereccl simpll
      cdiv cle idhmop hmopm mpan2 ad2antlr simplr hmopf nmopgt0 biimpa syl11anc
      recgt0 wi 0re ltle sylancr mpd leopnmid adantr leopmul2i syl32anc cc recn
      cmul reccl simpl wf1o hoif homulass mp3an3 recid2 opreq1d eqtr3d homulid2
      f1of ax-mp syl6eq breqtrd syldan 3impa ) ABCZAUAUBZDCZAUCEZFWKUJGZAHGZIJK
      ZWJWLQZWMWKLEZWPWJWMWRWLWJAUDCZWMWRAUEWSWRWMAUFUGMNWQWRQZWOWNWKIHGZHGZIJW
      TWNDCZWJXABCZLWNUKKZAXAJKZWOXBJKWLWRXCWJWKUHZOWJWLWRUIWLXDWJWRWLIBCXDULWK
      IUMUNUOWTLWNRKZXEWTWLLWKRKZXHWJWLWRUPWJWRXIWLWJPPASZWRXIAUQXJWRXIAURUSMNW
      KVAUTWLWRXHXEVBZWJWLWRQLDCXCXKVCXGLWNVDVEOVFWQXFWRAVGVHWNAXAVIVJWLWRXBITZ
      WJWLWKVKCZWRXLWKVLXMWRQZXBFIHGZIXNWNWKVMGZIHGZXBXOXNWNVKCZXMXQXBTZWKVNXMW
      RVOXRXMPPISZXSPPIVPXTVQPPIWDWEZWNWKIVRVSUTXNXPFIHWKVTWAWBXTXOITYAIWCWEWFM
      OWGWHWI $.
      $( [12-Aug-2006] $)
  $}

  ${
    $d u v T $.
    opsqrlem1.1 $e |- T e. HrmOp $.
    opsqrlem1.2 $e |- ( normop ` T ) e. RR $.
    opsqrlem1.3 $e |- 0hop <_op T $.
    opsqrlem1.4 $e |- R = ( ( 1 / ( normop ` T ) ) .op T ) $.
    opsqrlem1.5 $e |- ( T =/= 0hop -> E. u e. HrmOp ( 0hop <_op u /\
                      ( u o. u ) = R ) ) $.
    $( Lemma for opsqri . $)
    opsqrlem1 $p |- ( T =/= 0hop ->
                   E. v e. HrmOp ( 0hop <_op v /\ ( v o. v ) = T ) ) $=
      ( ch0o wbr ccom wceq wa cho wcel chot co chil ax-mp wne cv cleo wrex cnop
      cfv csqr cr cc0 cle wf hmopf nmopge0 hmopm mpan ad2antlr sqrge0i leopmuli
      sqrcli mpanr1 mpanl1 ad2ant2lr cc recni homulcl syl homco1 mp3an1 hmoplin
      syl11anc clo homco2 opreq2d cmul sqrthi opreq1i homulass mp3an12 syl5reqr
      fco 3eqtrd c1 cdiv id syl6eq wb nmlnopne0 recidzi opreq1d rerecclzi recnd
      sylbir mp3an13 homulid2 a1i 3eqtr3d sylan9eqr adantlr eqtrd adantrl breq2
      coeq1 coeq2 eqeq1d anbi12d rcla4ev syl12anc exp31 rexlimdv mpd ) DJUAZJBU
      BZUCKZXLXLLZCMZNZBOUDJAUBZUCKZXQXQLZDMZNZAOUDZIXKXPYBBOXKXLOPZXPYBXKYCNZX
      PNDUEUFZUGUFZXLQRZOPZJYGUCKZYGYGLZDMZYBYCYHXKXPYFUHPZYCYHUIYEUJKZYLSSDUKZ
      YMDOPZYNEDULTZDUMTZYEFUSTZYFXLUNUOUPYCXMYIXKXOYLYCXMYIYRYLYCNUIYFUJKZXMYI
      YMYSYQYEFUQTYFXLURUTVAVBYDXOYKXMYDXONYJYEXNQRZDYCYJYTMXKXOYCYJYFXLYGLZQRZ
      YFYFXNQRZQRZYTYCSSXLUKZSSYGUKZYJUUBMZXLULZYCUUEUUFUUHYFVCPZUUEUUFYFYRVDZY
      FXLVEUOVFUUIUUEUUFUUGUUJYFXLYGVGVHVJYCUUAUUCYFQYCXLVKPZUUEUUAUUCMZXLVIUUH
      UUIUUKUUEUULUUJYFXLXLVLVHVJVMYCYTYFYFVNRZXNQRZUUDUUMYEXNQYMUUMYEMYQYEFVOT
      VPYCSSXNUKZUUNUUDMZYCUUEUUEUUOUUHUUHSSSXLXLVTVJUUIUUIUUOUUPUUJUUJYFYFXNVQ
      VRVFVSWAUPXKXOYTDMYCXOXKYTYEWBYEWCRZDQRZQRZDXOXNUURYEQXOXNCUURXOWDHWEVMXK
      YEUUQVNRZDQRZWBDQRZUUSDXKUUTWBDQXKYEUIUAZUUTWBMDVKPZUVCXKWFYOUVDEDVITDWGT
      ZYEYEFVDZWHWLWIXKUUQVCPZUVAUUSMZXKUUQXKUVCUUQUHPUVEYEFWJWLWKYEVCPUVGYNUVH
      UVFYPYEUUQDVQWMVFUVBDMZXKYNUVIYPDWNTWOWPWQWRWSWTYAYIYKNAYGOXQYGMZXRYIXTYK
      XQYGJUCXAUVJXSYJDUVJXSYGXQLYJXQYGXQXBXQYGYGXCWSXDXEXFXGXHXIXJ $.
      $( [28-Jul-2011] $) $( [9-Aug-2006] $)
  $}

  ${
    $d v w G $.  $d v H $.  $d v w S $.  $d v w x y z T $.  $d j k F $.
    $d j k N $.
    opsqrlem2.1 $e |- T e. HrmOp $.
    opsqrlem2.2 $e |- S = { <. <. x , y >. , z >. | ( ( x e. HrmOp /\
    y e. HrmOp ) /\ z = ( x +op ( ( 1 / 2 ) .op ( T -op ( x o. x ) ) ) ) ) } $.
    opsqrlem2.3 $e |- F = ( S seq1 ( NN X. { 0hop } ) ) $.
    $( Lemma for opsqri . ` F `` N ` is the recursive function A_n (starting at
       n=1 instead of 0) of Theorem 9.4-2 of [Kreyszig] p. 476. $)
    opsqrlem2 $p |- ( F ` 1 ) = 0hop $=
      ( c1 cfv cn ch0o csn cxp cseq1 co fveq1i cho hmopex cv cdiv ccom oprabex2
      chod chot chos nnex snex xpex seq11 wcel wceq 1nn 0hmop elisseti fvconst2
      c2 ax-mp 3eqtri ) JFKJDLMNZOZPQZKJVBKZMJFVCIRDVBABCSSAUAZJURUBQEVEVEUCUEQ
      UFQUGQDTTHUDLVAUHMUIUJUKJLULVDMUMUNLMJMSUOUPUQUSUT $.
      $( [28-Jul-2011] $) $( [17-Aug-2006] $)

    $( Lemma for opsqri . $)
    opsqrlem3 $p |- F : NN --> HrmOp $=
      ( cn cho wf ch0o cxp co 0hmop wcel mp2an cv c2 csn cseq1 wss fconst snssi
      elisseti ax-mp c1 cdiv ccom chod chot chos wceq eqid hmopco mp3an3 anidms
      fss hmopd mpan cr 2re 2ne0 rereccli hmopm 3syl hmops adantr foprab hmopex
      mpdan oprabex2 nnex snex xpex seq1f feq1i mpbir ) JKFLJKDJMUAZNZUBOZLZJKW
      ALZKKNKDLWCJVTWALVTKUCZWDJMMKPUFUDMKQWEPMKUEUGJVTKWAUSRABCKKASZUHTUIOZEWF
      WFUJZUKOZULOZUMOZKDHWFKQZWKKQZBSKQWLWJKQZWMWLWHKQZWIKQZWNWLWOWLWLWHWHUNWO
      WHUOWFWFUPUQUREKQWOWPGEWHUTVAWGVBQWPWNTVCVDVEWGWIVFVAVGWFWJVHVLVIVJKDWAAB
      CKKWKDVKVKHVMJVTVNMVOVPVQRJKFWBIVRVS $.
      $( [28-Jul-2011] $) $( [17-Aug-2006] $)

    $( Lemma for opsqri . $)
    opsqrlem4 $p |- ( ( G e. HrmOp /\ H e. HrmOp ) ->
              ( G S H ) = ( G +op ( ( 1 / 2 ) .op ( T -op ( G o. G ) ) ) ) ) $=
      ( cv co ccom chod chot chos wceq cho opreq2d vw vv c1 c2 cdiv coeq1 coeq2
      opreq1 id eqtrd opreq12d eqeq12d opreq2 eqeq1d oprex oprabval2 vtocl2ga
      eqidd ) UALZUBLZDMZUSUCUDUEMZEUSUSNZOMZPMZQMZRGUTDMZGVBEGGNZOMZPMZQMZRGHD
      MZVKRUAUBGHSSUSGRZVAVGVFVKUSGUTDUHVMUSGVEVJQVMUIVMVDVIVBPVMVCVHEOVMVCGUSN
      VHUSGUSUFUSGGUGUJTTUKULUTHRVGVLVKUTHGDUMUNABCUSUTSSALZVBEVNVNNZOMZPMZQMVF
      DVFUSVEQUOVNUSRZVNUSVQVEQVRUIVRVPVDVBPVRVOVCEOVRVOUSVNNVCVNUSVNUFVNUSUSUG
      UJTTUKBLUTRVFURJUPUQ $.
      $( [28-Jul-2011] $) $( [22-Aug-2006] $)

    $( Lemma for opsqri . $)
    opsqrlem5 $p |- ( N e. NN -> ( F ` ( N + 1 ) ) =
  ( ( F ` N ) +op ( ( 1 / 2 ) .op ( T -op ( ( F ` N ) o. ( F ` N ) ) ) ) ) ) $=
      ( cn wcel c1 co cfv ch0o ccom chod wceq cho caddc csn cxp cseq1 cdiv chot
      c2 chos fveq1i cv hmopex oprabex2 nnex snex xpex seq1p1 opreq1i opsqrlem3
      a1i ffvelrni peano2nn 0hmop elisseti fvconst2 syl6eqel opsqrlem4 syl11anc
      syl syl5eqr 3eqtrd ) GKLZGMUANZFOZVLDKPUBZUCZUDNZOZGVPOZVLVOOZDNZGFOZMUGU
      ENZEWAWAQRNUFNUHNZVMVQSVKVLFVPJUIUSGDVOABCTTAUJZWBEWDWDQRNUFNUHNDUKUKIULK
      VNUMPUNUOUPVKVTWAVSDNZWCWAVRVSDGFVPJUIUQVKWATLVSTLWEWCSKTGFABCDEFHIJURUTV
      KVSPTVKVLKLVSPSGVAKPVLPTVBVCVDVHVBVEABCDEFWAVSHIJVFVGVIVJ $.
      $( [28-Jul-2011] $) $( [17-Aug-2006] $)

    ${
      opsqrlem6.4 $e |- T <_op Iop $.
      $( Lemma for opsqri . $)
      opsqrlem6 $p |- ( N e. NN -> ( F ` N ) <_op Iop ) $=
        ( chio cleo co wceq wcel chod c2 chos chil vj vk cv cfv wbr caddc fveq2
        c1 breq1d ch0o opsqrlem2 idleop eqbrtri cn chot ccom opsqrlem3 ffvelrni
        cho idhmop hmopd mpan syl eqid hmopco mp3an3 syl11anc leopsq 3syl leop3
        wb mp2an mpbi wa leopadd mpanl2 mpanr2 cdiv hmopf fco ax-mp hosubcl 2cn
        2ne0 reccli homulcl hoadddi mp3an1 cmul homulass mp3an12 recidi opreq1i
        wf cc homulid2 syl5eq eqtr3d opreq2d eqtrd hoaddcl hosubdi ho2times a1i
        hoaddsubass hoadd32 mp3an13 opreq1d mp3an23 hosubsub4 3eqtr3d hosubadd4
        3eqtr4rd mpanr1 mpanl1 3eqtr2d hocsubdir hmoplin hoid1i hoico2 opreq12d
        clo hoddi mp3an2 hoico1 jctil syl12anc 3eqtrd opsqrlem5 3eqtr4d breqtrd
        peano2nn cr cc0 clt 2re 2pos leopmul mpbird mpan2 nn1suc ) UAUCZFUDZLMU
        EUHFUDZLMUEUBUCZUHUFNZFUDZLMUEZGFUDZLMUEUAUBGUUBUHOUUCUUDLMUUBUHFUGUIUU
        BUUFOUUCUUGLMUUBUUFFUGUIUUBGOUUCUUILMUUBGFUGUIUUDUJLMABCDEFHIJUKULUMUUE
        UNPZUUHUJLUUGQNZMUEZUUJUULUJRUUKUONZMUEZUUJUJLUUEFUDZQNZUUPUPZLEQNZSNZU
        UMMUUJUUQUSPZUJUUQMUEZUJUUSMUEZUUJUUPUSPZUVCUUTUUJUUOUSPZUVCUNUSUUEFABC
        DEFHIJUQZURZLUSPZUVDUVCUTLUUOVAVBZVCZUVIUVCUVCUUQUUQOUUTUUQVDUUPUUPVEVF
        VGUUJUVDUVCUVAUVFUVHUUPVHVIUUTUVAUJUURMUEZUVBELMUEZUVJKEUSPZUVGUVKUVJVK
        HUTELVJVLVMUUTUURUSPZUVAUVJVNUVBUVGUVLUVMUTHLEVAVLUUQUURVOVPVQVGUUJLUUO
        UUOUPZRUUOUONZQNZSNZUURSNZRLUUOUHRVRNZEUVNQNZUONZSNZQNZUONZUUSUUMUUJRLU
        ONZRUWBUONZQNZUWEUVOUVTSNZQNZUWDUVRUUJUWFUWHUWEQUUJUWFUVORUWAUONZSNZUWH
        UUJTTUUOWNZTTUWAWNZUWFUWKOZUUJUVDUWLUVFUUOVSZVCZUUJTTUVNWNZTTUVTWNZUWMU
        UJUWLUWLUWQUWPUWPTTTUUOUUOVTVGZTTEWNZUWQUWRUVLUWTHEVSWAZEUVNWBVBZUVSWOP
        ZUWRUWMRWCWDWEZUVSUVTWFVBVIZRWOPZUWLUWMUWNWCRUUOUWAWGWHVGUUJUWJUVTUVOSU
        UJRUVSWINZUVTUONZUWJUVTUUJUWQUWRUXHUWJOZUWSUXBUXFUXCUWRUXIWCUXDRUVSUVTW
        JWKVIUUJUXHUHUVTUONZUVTUXGUHUVTUORWCWDWLWMUUJUWQUWRUXJUVTOUWSUXBUVTWPVI
        WQWRWSWTWSUUJTTUWBWNZUWDUWGOZUUJUWLUWMUXKUWPUXEUUOUWAXAVGUXFTTLWNZUXKUX
        LWCUVGUXMUTLVSWAZRLUWBXBWKVCUUJUVRUWEUVNSNZUVOESNQNZUWEUVOQNUVTQNZUWIUU
        JUVQLSNZEQNZUXOUVOQNZEQNZUVRUXPUUJUXRUXTEQUUJUWEUVPSNZLLSNZUVPSNZUXTUXR
        UYBUYDOUUJUWEUYCUVPSUXMUWEUYCOUXNLXCWAWMXDUUJUWQTTUVOWNZUXTUYBOZUWSUUJU
        VDUWLUYEUVFUWOUXFUWLUYEWCRUUOWFVBVIZTTUWEWNZUWQUYEUYFUXFUXMUYHWCUXNRLWF
        VLZUWEUVNUVOXEWHVGUUJTTUVPWNZUXRUYDOZUUJUWQUYEUYJUWSUYGUVNUVOWBVGZUXMUY
        JUXMUYKUXNUXNLUVPLXFXGVCXMXHUUJUYJTTUVQWNZUXSUVROZUYLUXMUYJUYMUXNLUVPXA
        VBUYMUXMUWTUYNUXNUXAUVQLEXEXIVIUUJTTUXOWNZUYEUYAUXPOZUUJUWQUYOUWSUYHUWQ
        UYOUYIUWEUVNXAVBVCUYGUYOUYEUWTUYPUXAUXOUVOEXJVFVGXKUUJUYEUWQUXQUXPOZUYG
        UWSUYHUYEUWQUYQUYIUYHUYEVNUWTUWQUYQUXAUWEUVOEUVNXLXNXOVGUUJUYEUWRUXQUWI
        OZUYGUUJUWQUWRUWSUXBVCUYHUYEUWRUYRUYIUWEUVOUVTXJWHVGXPXMUUJUUQUVQUURSUU
        JUUQLUUPUPZUUOUUPUPZQNZLUVNSNZUUOUUOSNZQNZUVQUUJUWLTTUUPWNZUUQVUAOZUWPU
        UJUVDUWLVUEUVFUWOUXMUWLVUEUXNLUUOWBVBVIUXMUWLVUEVUFUXNLUUOUUPXQWHVGUUJV
        UAUUPUUOUVNQNZQNZVUDUUJUYSUUPUYTVUGQUUJUYSLLUPZLUUOUPZQNZUUPUUJUVDUWLUY
        SVUKOZUVFUWOLYBPZUXMUWLVULUVGVUMUTLXRWAUXNLLUUOYCWKVIUUJVUILVUJUUOQVUIL
        OUUJLUXNXSXDUUJUVDUWLVUJUUOOUVFUWOUUOXTVIYAWTUUJUYTUUOLUPZUVNQNZVUGUUJU
        UOYBPZUWLUYTVUOOZUUJUVDVUPUVFUUOXRVCUWPVUPUXMUWLVUQUXNUUOLUUOYCYDVGUUJV
        UNUUOUVNQUUJUVDUWLVUNUUOOUVFUWOUUOYEVIXHWTYAUUJUXMUWLVNUWLUWQVUHVUDOUUJ
        UWLUXMUWPUXNYFUWPUWSLUUOUUOUVNXLYGWTUUJVUBUVOQNZVUDUVQUUJUVOVUCVUBQUUJU
        VDUWLUVOVUCOUVFUWOUUOXCVIWSUUJUWQUYEVURUVQOZUWSUYGUXMUWQUYEVUSUXNLUVNUV
        OXEWHVGWRYHXHUUJUUKUWCRUOUUJUUGUWBLQABCDEFUUEHIJYIWSWSYJYKUUJUUGUSPZUUK
        USPZUULUUNVKZUUJUUFUNPZVUTUUEYLZUNUSUUFFUVEURZVCUVGVUTVVAUTLUUGVAVBRYMP
        VVAYNRYOUEVVBYPYQRUUKYRXGVIYSUUJVVCVUTUUHUULVKZVVDVVEVUTUVGVVFUTUUGLVJY
        TVIYSUUA $.
        $( [28-Jul-2011] $) $( [23-Aug-2006] $)
    $}
  $}

$(
  @{
    opsqrlem8.1 @e |- T e. HrmOp @.
    opsqrlem8.2 @e |- 0hop <_op T @.
    opsqrlem8.3 @e |- T <_op Iop @.
    @( Lemma for ~ opsqri . @)
    opsqrlem8 @p |- E. u e. HrmOp ( 0hop <_op u /\
                    ( u o. u ) = T ) @=
      (  ) ? @.
      @( [ ?] @) @( [17-Aug-2006] @)
  @}

  @{
    @d u T @.
    @( Lemma for ~ opsqri . @)
    opsqrlem9 @p |- ( ( T e. HrmOp /\ 0hop <_op T /\ T <_op Iop ) ->
                 E. u e. HrmOp ( 0hop <_op u /\ ( u o. u ) = T ) ) @=
      ( cho wcel ch0o cleo wbr chio w3a cv ccom wceq wa wrex cif eqeq2 anbi2d
      rexbidv eleq1 breq2 breq1 3anbi123d 0hmop 0leop idleop 3pm3.2i elimhyp
      simp1i simp2i simp3i opsqrlem8 dedth ) BCDZEBFGZBHFGZIZEAJZFGZUQUQKZBL
      ZMZACNURUSUPBEOZLZMZACNBEBVBLZVAVDACVEUTVCURBVBUSPQRAVBVBCDZEVBFGZVBHFGZU
      PVFVGVHIECDZEEFGZEHFGZIBEVEUMVFUNVGUOVHBVBCSBVBEFTBVBHFUAUBEVBLVIVFVJVGVK
      VHEVBCSEVBEFTEVBHFUAUBVIVJVKUCUDUEUFUGZUHVFVGVHVLUIVFVGVHVLUJUKUL @.
      @( [ ?] @) @( [8-Aug-2006] @)
  @}

  @{
    @d u v T @.
    opsqr.1 @e |- T e. HrmOp @.
    opsqr.2 @e |- ( normop ` T ) e. RR @.
    opsqr.3 @e |- 0hop <_op T @.
    @( Square root lemma. @)
    opsqri @p |- E. u e. HrmOp ( 0hop <_op u /\
                    ( u o. u ) = T ) @=
      ( vv ch0o wceq cv cleo wbr ccom wa cho wrex eqid eqeq2 anbi2d rexbidv
      wne c1 cnop cfv cdiv co chot wcel chio opsqrlem9 cr cc0 clo hmoplin
      ax-mp nmlnop0iHIL biimpi necon3i rerecclzi syl hmopm mpan2 cle leopmuli
  mpanr2 mpanl2 clt wi 0re ltle mpan nmlnopgt0i recgt0i sylbi sylcOLD sylancOLD
    nmopleid mp3an12 syl3ancOLD opsqrlem1 adantl 0hmop 0leop ho0f ho0coi pm3.2i
      breq2 coeq1 coeq2 eqtrd eqeq1d anbi12d rcla4ev mp2an a1i pm2.61ne ) GGHZG
      AIZJKZWQWQLZBHZMZANOZGPWPXBWRWSGHZMZANOZBGBGHZXAXDANXFWTXCWRBGWSQRSBGTZXB
      WPAFUABUBUCZUDUEZBUFUEZBCDEXJPXJNUGZGXJJKZXJUHJKZGFIZJKXNXNLXJHMFNOXGFXJU
      IXGXIUJUGZXKXGXHUKTXOXHUKBGXHUKHXFBBNUGZBULUGCBUMUNZUOUPUQXHDURUSZXOXPXKC
      XIBUTVAUSXOUKXIVBKZXLXGXOXPXSXLCXOXPMXSGBJKXLEXIBVCVDVEXRXOUKXIVFKZXSXGUK
      UJUGXOXTXSVGVHUKXIVIVJXRXGUKXHVFKXTBXQVKXHDVLVMVNVOXPXHUJUGXGXMCDBVPVQVRV
      SVTXEWPGNUGGGJKZGGLZGHZMZXEWAYAYCWBGWCWDWEXDYDAGNWQGHZWRYAXCYCWQGGJWFYEWS
      YBGYEWSGWQLYBWQGWQWGWQGGWHWIWJWKWLWMWNWOUN @.
      @( [ ?] @) @( [8-Aug-2006] @)
  @}
$)

