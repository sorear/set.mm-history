$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      States on a Hilbert lattice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f x y $.
    $( Define the set of states on a Hilbert lattice.  Definition of [Kalmbach]
       p. 266. $)
    df-st $a |- States = { f e. ( ( 0 [,] 1 ) ^m CH ) | ( ( f ` ~H ) = 1 /\
          A. x e. CH A. y e. CH ( x C_ ( _|_ ` y ) ->
            ( f ` ( x vH y ) ) = ( ( f ` x ) + ( f ` y ) ) ) ) } $.

    $( Define the set of complex Hilbert-space-valued states on a Hilbert
       lattice.  Definition of CH-states in [Mayet3] p. 9. $)
    df-hst $a |- CHStates = { f e. ( ~H ^m CH ) |
        ( ( normh ` ( f ` ~H ) ) = 1 /\ A. x e. CH A. y e. CH
          ( x C_ ( _|_ ` y ) -> ( ( ( f ` x ) .ih ( f ` y ) ) = 0 /\
              ( f ` ( x vH y ) ) = ( ( f ` x ) +h ( f ` y ) ) ) ) ) } $.
  $}

  ${
    $d x y z w f h H $.  $d x y z w A $.  $d x y f S $.  $d y B $.
    $( Property of a state.  (Revised by Mario Carneiro, 23-Dec-2013.) $)
    isst $p |- ( S e. States <-> ( S : CH --> ( 0 [,] 1 ) /\
            ( S ` ~H ) = 1 /\ A. x e. CH A. y e. CH ( x C_ ( _|_ ` y ) ->
            ( S ` ( x vH y ) ) = ( ( S ` x ) + ( S ` y ) ) ) ) ) $=
      ( vf cc0 c1 cicc co cch cmap wcel chil cfv wceq cv cort wss chj caddc wi
      wral wa wf cst w3a oprex chex elmap anbi1i fveq1 eqeq1d opreq12d eqeq12d
      imbi2d 2ralbidv anbi12d df-st elrab2 3anass 3bitr4i ) CEFGHZIJHZKZLCMZFNZ
      AOZBOZPMQZVFVGRHZCMZVFCMZVGCMZSHZNZTZBIUAAIUAZUBZUBIVACUCZVQUBCUDKVRVEVPU
      EVCVRVQVAICEFGUFUGUHUILDOZMZFNZVHVIVSMZVFVSMZVGVSMZSHZNZTZBIUAAIUAZUBVQDC
      VBUDVSCNZWAVEWHVPWIVTVDFLVSCUJUKWIWGVOABIIWIWFVNVHWIWBVJWEVMVIVSCUJWIWCVK
      WDVLSVFVSCUJVGVSCUJULUMUNUOUPABDUQURVRVEVPUSUT $.
      $( [23-Dec-2013] $) $( [23-Oct-1999] $)

    $( Property of a complex Hilbert-space-valued state.  Definition of
       CH-states in [Mayet3] p. 9. $)
    ishst $p |- ( S e. CHStates <-> ( S : CH --> ~H /\
          ( normh ` ( S ` ~H ) ) = 1 /\
          A. x e. CH A. y e. CH ( x C_ ( _|_ ` y ) ->
          ( ( ( S ` x ) .ih ( S ` y ) ) = 0 /\
            ( S ` ( x vH y ) ) = ( ( S ` x ) +h ( S ` y ) ) ) ) ) ) $=
      ( vf chil cch cmap co wcel cfv cno c1 wceq cv cort wss csp cc0 chj cva
      wa wi wral wf chst w3a ax-hilex chex elmap anbi1i fveq1 fveq2d eqeq1d
      opreq12d eqeq12d anbi12d imbi2d 2ralbidv df-hst elrab2 3anass 3bitr4i )
      CEFGHZIZECJZKJZLMZANZBNZOJPZVHCJZVICJZQHZRMZVHVISHZCJZVKVLTHZMZUAZUBZBFUC
      AFUCZUAZUAFECUDZWBUACUEIWCVGWAUFVDWCWBEFCUGUHUIUJEDNZJZKJZLMZVJVHWDJZVIWD
      JZQHZRMZVOWDJZWHWITHZMZUAZUBZBFUCAFUCZUAWBDCVCUEWDCMZWGVGWQWAWRWFVFLWRWEV
      EKEWDCUKULUMWRWPVTABFFWRWOVSVJWRWKVNWNVRWRWJVMRWRWHVKWIVLQVHWDCUKZVIWDCUK
      ZUNUMWRWLVPWMVQVOWDCUKWRWHVKWIVLTWSWTUNUOUPUQURUPABDUSUTWCVGWAVAVB $.
      $( [23-Dec-2013] $) $( [25-Jun-2006] $)

    $( ` [ 0 , 1 ] ` closure of the value of a state.  (Contributed by Mario
       Carneiro, 23-Dec-2013.) $)
    sticl $p |- ( S e. States -> ( A e. CH -> ( S ` A ) e. ( 0 [,] 1 ) ) ) $=
      ( vx vy cst wcel cch cc0 c1 cicc co wf cfv wi chil wceq cv cort wss chj
      caddc wral isst simp1bi ffvelrn ex syl ) BEFZGHIJKZBLZAGFZABMUIFZNUHUJOBM
      IPCQZDQZRMSUMUNTKBMUMBMUNBMUAKPNDGUBCGUBCDBUCUDUJUKULGUIABUEUFUG $.
      $( [23-Dec-2013] $)

    $( Real closure of the value of a state.  (Revised by Mario Carneiro,
       23-Dec-2013.) $)
    stcl $p |- ( S e. States -> ( A e. CH -> ( S ` A ) e. RR ) ) $=
      ( cst wcel cch cfv cc0 c1 cicc co cr sticl wss 0re 1re iccssre mp2an
      sseli syl6 ) BCDAEDABFZGHIJZDTKDABLUAKTGKDHKDUAKMNOGHPQRS $.
      $( [23-Dec-2013] $) $( [24-Oct-1999] $)

    $( Closure of the value of a Hilbert-space-valued state. $)
    hstcl $p |- ( ( S e. CHStates /\ A e. CH ) -> ( S ` A ) e. ~H ) $=
      ( vx vy chst wcel cch chil wf cfv cno c1 wceq cv cort wss csp co cc0 chj
      cva wa wi wral ishst simp1bi ffvelrn sylan ) BEFZGHBIZAGFABJHFUIUJHBJKJLM
      CNZDNZOJPUKBJZULBJZQRSMUKULTRBJUMUNUARMUBUCDGUDCGUDCDBUEUFGHABUGUH $.
      $( [25-Jun-2006] $)

    $( Unit value of a Hilbert-space-valued state. $)
    hst1a $p |- ( S e. CHStates -> ( normh ` ( S ` ~H ) ) = 1 ) $=
      ( vx vy chst wcel cch chil wf cfv cno c1 wceq cv cort wss csp co cc0 chj
      cva wa wi wral ishst simp2bi ) ADEFGAHGAIJIKLBMZCMZNIOUFAIZUGAIZPQRLUFUGS
      QAIUHUITQLUAUBCFUCBFUCBCAUDUE $.
      $( [25-Jun-2006] $)

    $( Properties of a Hilbert-space-valued state. $)
    hstel2 $p |- ( ( ( S e. CHStates /\ A e. CH ) /\ ( B e. CH
       /\ A C_ ( _|_ ` B ) ) ) -> ( ( ( S ` A ) .ih ( S ` B ) ) = 0 /\
                    ( S ` ( A vH B ) ) = ( ( S ` A ) +h ( S ` B ) ) ) ) $=
      ( vx vy chst wcel cch wa cort cfv wss cv csp co cc0 wceq chj cva wi wral
      chil wf cno c1 ishst simp3bi ad2antrr sseq1 fveq2 opreq1d eqeq1d opreq1
      fveq2d eqeq12d anbi12d imbi12d sseq2d opreq2d opreq2 rcla42v com23 impr
      adantll mpd ) CFGZAHGZIBHGZABJKZLZIZIDMZEMZJKZLZVLCKZVMCKZNOZPQZVLVMROZCK
      ZVPVQSOZQZIZTZEHUADHUAZACKZBCKZNOZPQZABROZCKZWGWHSOZQZIZVFWFVGVKVFHUBCUCU
      BCKUDKUEQWFDECUFUGUHVGVKWFWOTZVFVGVHVJWPVGVHIWFVJWOWEVJWOTAVNLZWGVQNOZPQZ
      AVMROZCKZWGVQSOZQZIZTDEABHHVLAQZVOWQWDXDVLAVNUIXEVSWSWCXCXEVRWRPXEVPWGVQN
      VLACUJZUKULXEWAXAWBXBXEVTWTCVLAVMRUMUNXEVPWGVQSXFUKUOUPUQVMBQZWQVJXDWOXGV
      NVIAVMBJUJURXGWSWJXCWNXGWRWIPXGVQWHWGNVMBCUJZUSULXGXAWLXBWMXGWTWKCVMBARUT
      UNXGVQWHWGSXHUSUOUPUQVAVBVCVDVE $.
      $( [25-Jun-2006] $)

    $( Orthogonality property of a Hilbert-space-valued state.  This is a key
       feature distinguishing it from a real-valued state. $)
    hstorth $p |- ( ( ( S e. CHStates /\ A e. CH ) /\ ( B e. CH
       /\ A C_ ( _|_ ` B ) ) ) -> ( ( S ` A ) .ih ( S ` B ) ) = 0 ) $=
      ( chst wcel cch wa cort cfv wss csp co cc0 wceq chj cva hstel2 simpld )
      CDEAFEGBFEABHIJGGACIZBCIZKLMNABOLCISTPLNABCQR $.
      $( [25-Jun-2006] $)

    $( Orthogonal sum property of a Hilbert-space-valued state. $)
    hstosum $p |- ( ( ( S e. CHStates /\ A e. CH ) /\ ( B e. CH
                         /\ A C_ ( _|_ ` B ) ) ) ->
                    ( S ` ( A vH B ) ) = ( ( S ` A ) +h ( S ` B ) ) ) $=
      ( chst wcel cch wa cort cfv wss csp co cc0 wceq chj cva hstel2 simprd )
      CDEAFEGBFEABHIJGGACIZBCIZKLMNABOLCISTPLNABCQR $.
      $( [25-Jun-2006] $)

    $( Sum of a Hilbert-space-valued state of a lattice element and its
       orthocomplement. $)
    hstoc $p |- ( ( S e. CHStates /\ A e. CH ) ->
                  ( ( S ` A ) +h ( S ` ( _|_ ` A ) ) ) = ( S ` ~H ) ) $=
      ( chst wcel cch wa cort cfv chj co cva chil wss wceq choccl adantl csh
      chsh shococss syl jca hstosum mpdan chjo fveq2d eqtr3d ) BCDZAEDZFZAAGHZI
      JZBHZABHUJBHKJZLBHZUIUJEDZAUJGHMZFULUMNUIUOUPUHUOUGAOPUHUPUGUHAQDUPARASTP
      UAAUJBUBUCUHULUNNUGUHUKLBAUDUEPUF $.
      $( [25-Jun-2006] $)

    $( Sum of norms of a Hilbert-space-valued state. $)
    hstnmoc $p |- ( ( S e. CHStates /\ A e. CH ) -> ( ( ( normh `
         ( S ` A ) ) ^ 2 ) + ( ( normh ` ( S ` ( _|_ ` A ) ) ) ^ 2 ) ) = 1 ) $=
      ( chst wcel cch wa cfv cort cva co cno c2 cexp chil caddc c1 hstoc
      fveq2d opreq1d csp cc0 wceq hstcl choccl sylan2 jca wss adantl csh chsh
      shococss syl hstorth mpdan normpyth sylc hst1a sq1 syl6eq adantr 3eqtr3d
      ) BCDZAEDZFZABGZAHGZBGZIJZKGZLMJZNBGZKGZLMJZVEKGLMJVGKGLMJOJZPVDVIVLLMVDV
      HVKKABQRSVDVENDZVGNDZFVEVGTJUAUBZVJVNUBVDVOVPABUCVCVBVFEDZVPAUDZVFBUCUEUF
      VDVRAVFHGUGZFVQVDVRVTVCVRVBVSUHVCVTVBVCAUIDVTAUJAUKULUHUFAVFBUMUNVEVGUOUP
      VBVMPUBVCVBVMPLMJPVBVLPLMBUQSURUSUTVA $.
      $( [25-Jun-2006] $)

    $( The value of a state is nonnegative.  (Revised by Mario Carneiro,
       23-Dec-2013.) $)
    stge0 $p |- ( S e. States -> ( A e. CH -> 0 <_ ( S ` A ) ) ) $=
      ( cst wcel cch cfv cc0 c1 cicc co cle wbr sticl cr w3a wb 0re 1re elicc2
      mp2an simp2bi syl6 ) BCDAEDABFZGHIJDZGUCKLZABMUDUCNDZUEUCHKLZGNDHNDUDUFUE
      UGOPQRGHUCSTUAUB $.
      $( [23-Dec-2013] $) $( [24-Oct-1999] $)

    $( The value of a state is less than or equal to one.  (Revised by Mario
       Carneiro, 23-Dec-2013.) $)
    stle1 $p |- ( S e. States -> ( A e. CH -> ( S ` A ) <_ 1 ) ) $=
      ( cst wcel cch cfv cc0 c1 cicc co cle wbr sticl cr w3a wb 0re 1re elicc2
      mp2an simp3bi syl6 ) BCDAEDABFZGHIJDZUCHKLZABMUDUCNDZGUCKLZUEGNDHNDUDUFUG
      UEOPQRGHUCSTUAUB $.
      $( [23-Dec-2013] $) $( [24-Oct-1999] $)

    $( The norm of the value of a Hilbert-space-valued state is less than or
       equal to one. $)
    hstle1 $p |- ( ( S e. CHStates /\ A e. CH ) ->
                   ( normh ` ( S ` A ) ) <_ 1 ) $=
      ( chst wcel cch wa cfv cno c1 cle wbr c2 cexp co cort caddc cc0 cr chil
      choccl hstcl sylan2 normcl syl sqge0 wb resqcl addge01 syl11anc mpbid
      hstnmoc sq1 syl6eqr breqtrd normge0 1re 0re lt01 ltleii le2sq mpanr12
      mpbird ) BCDZAEDZFZABGZHGZIJKZVGLMNZILMNZJKZVEVIVIAOGZBGZHGZLMNZPNZVJJVEQ
      VOJKZVIVPJKZVEVNRDZVQVEVMSDZVSVDVCVLEDVTATVLBUAUBVMUCUDZVNUEUDVEVIRDZVORD
      ZVQVRUFVEVGRDZWBVEVFSDZWDABUAZVFUCUDZVGUGUDVEVSWCWAVNUGUDVIVOUHUIUJVEVPIV
      JABUKULUMUNVEWDQVGJKZVHVKUFZWGVEWEWHWFVFUOUDWDWHFIRDQIJKWIUPQIUQUPURUSVGI
      UTVAUIVB $.
      $( [25-Jun-2006] $)

    $( The norm of a Hilbert-space-valued state equals one iff the state value
       equals the state value of the lattice unit. $)
    hst1h $p |- ( ( S e. CHStates /\ A e. CH ) ->
                  ( ( normh ` ( S ` A ) ) = 1 <-> ( S ` A ) = ( S ` ~H ) ) ) $=
      ( chst wcel cch wa cfv cno c1 wceq chil cort cva co c0v c2 cexp cc0 cmin
      caddc cc ax-1cn cr choccl hstcl sylan2 normcl syl resqcl recnd pncan2
      sylancr adantr opreq1 sq1 syl6req opreq1d hstnmoc sylan9eqr eqtr3d
      subidi syl6eq ex wb sqeq0 norm-i bitrd sylibd imp opreq2d hstoc
      ax-hvaddid 3eqtr3rd fveq2 hst1a impbida ) BCDZAEDZFZABGZHGZIJZVTKBGZJZVSW
      BFZVTALGZBGZMNZVTOMNZWCVTWEWGOVTMVSWBWGOJZVSWBWGHGZPQNZRJZWJVSWBWMWEWLIIS
      NZRWEIWLTNZISNZWLWNVSWPWLJZWBVSIUADWLUADWQUBVSWLVSWKUCDZWLUCDVSWGKDZWRVRV
      QWFEDWSAUDWFBUEUFZWGUGUHZWKUIUHUJIWLUKULUMWEWOIISWBVSWOWAPQNZWLTNIWBIXBWL
      TWBXBIPQNIWAIPQUNUOUPUQABURUSUQUTIUBVAVBVCVSWMWKRJZWJVSWKUADWMXCVDVSWKXAU
      JWKVEUHVSWSXCWJVDWTWGVFUHVGVHVIVJVSWHWCJWBABVKUMVSWIVTJZWBVSVTKDXDABUEVTV
      LUHUMVMWDVSWAWCHGZIVTWCHVNVQXEIJVRBVOUMUSVP $.
      $( [9-Mar-2007] $) $( [25-Jun-2006] $)

    $( The norm of a Hilbert-space-valued state equals zero iff the state value
       equals zero. $)
    hst0h $p |- ( ( S e. CHStates /\ A e. CH ) ->
                  ( ( normh ` ( S ` A ) ) = 0 <-> ( S ` A ) = 0h ) ) $=
      ( chst wcel cch wa cfv chil cno cc0 wceq c0v wb hstcl norm-i syl ) BCDAED
      FABGZHDQIGJKQLKMABNQOP $.
      $( [30-Jun-2006] $)

    $( Pythagorean property of a Hilbert-space-valued state for orthogonal
       vectors ` A ` and ` B ` . $)
    hstpyth $p |- ( ( ( S e. CHStates /\ A e. CH ) /\ ( B e. CH /\
               A C_ ( _|_ ` B ) ) ) -> ( ( normh ` ( S ` ( A vH B ) ) ) ^ 2 ) =
         ( ( ( normh ` ( S ` A ) ) ^ 2 ) + ( ( normh ` ( S ` B ) ) ^ 2 ) ) ) $=
      ( chst wcel cch wa cort cfv wss chj co cno c2 cexp cva caddc hstosum
      fveq2d opreq1d chil csp cc0 wceq hstcl adantr ad2ant2r hstorth normpyth
      3impia syl111anc eqtrd ) CDEZAFEZGZBFEZABHIJZGZGZABKLCIZMIZNOLACIZBCIZPLZ
      MIZNOLZVBMINOLVCMINOLQLZUSVAVENOUSUTVDMABCRSTUSVBUAEZVCUAEZVBVCUBLUCUDZVF
      VGUDZUOVHURACUEUFUMUPVIUNUQBCUEUGABCUHVHVIVJVKVBVCUIUJUKUL $.
      $( [26-Jun-2006] $)

    $( Ordering property of a Hilbert-space-valued state. $)
    hstle $p |- ( ( ( S e. CHStates /\ A e. CH ) /\ ( B e. CH /\ A C_ B ) )
                   -> ( normh ` ( S ` A ) ) <_ ( normh ` ( S ` B ) ) ) $=
      ( chst wcel cch wa wss cfv cno cle wbr c2 cexp co c1 caddc cort wceq
      hstnmoc adantlr opreq2d cc cr chil hstcl normcl syl resqcl adantr recnd
      choccl sylan2 add12 syl111anc eqtr3d adantrr chj ococ sseq2d biimpar jca
      hstpyth cc0 chjcl anassrs normge0 hstle1 1re le2sq2 mpanr1 syl21anc sq1
      syl6breq eqbrtrrd wb readdcl syl11anc leadd2 mp3an2 mpbid eqbrtrd leadd1
      mp3an3 mpbird le2sq ) CDEZAFEZGZBFEZABHZGZGZACIZJIZBCIZJIZKLZWOMNOZWQMNOZ
      KLZWMXAWSPQOZWTPQOZKLZWMXBWTWSBRIZCIZJIZMNOZQOZQOZXCKWIWJXBXJSWKWIWJGZWSW
      TXHQOZQOZXBXJXKXLPWSQWGWJXLPSWHBCTUAUBXKWSUCEWTUCEXHUCEXMXJSXKWSWIWSUDEZW
      JWIWOUDEZXNWIWNUEEZXOACUFZWNUGUHZWOUIUHUJZUKXKWTWGWJWTUDEZWHWGWJGZWQUDEZX
      TYAWPUEEZYBBCUFZWPUGUHZWQUIUHUAZUKXKXHWGWJXHUDEZWHYAXGUDEZYGYAXFUEEZYHWJW
      GXEFEZYIBULZXECUFUMXFUGUHXGUIUHUAZUKWSWTXHUNUOUPUQWMXIPKLZXJXCKLZWMAXEURO
      ZCIZJIZMNOZXIPKWLWIYJAXERIZHZGYRXISWLYJYTWJYJWKYKUJWJYTWKWJYSBABUSUTVAVBA
      XECVCUMWIWJYRPKLWKXKYRPMNOZPKXKYQUDEZVDYQKLZYQPKLZYRUUAKLZXKYPUEEZUUBWGWH
      WJUUFWHWJGZWGYOFEZUUFWJWHYJUUHYKAXEVEUMZYOCUFUMVFZYPUGUHXKUUFUUCUUJYPVGUH
      WGWHWJUUDUUGWGUUHUUDUUIYOCVHUMVFUUBUUCGPUDEZUUDUUEVIYQPVJVKVLVMVNUQVOWIWJ
      YMYNVPZWKXKXIUDEZXTUULXKXNYGUUMXSYLWSXHVQVRYFUUMUUKXTUULVIXIPWTVSVTVRUQWA
      WBWIWJXAXDVPZWKXKXNXTUUNXSYFXNXTUUKUUNVIWSWTPWCWDVRUQWEWIWJWRXAVPZWKXKXOV
      DWOKLZGZYBVDWQKLZGZUUOWIUUQWJWIXOUUPXRWIXPUUPXQWNVGUHVBUJWGWJUUSWHYAYBUUR
      YEYAYCUURYDWPVGUHVBUAWOWQWFVRUQWE $.
      $( [26-Jun-2006] $)

    $( Ordering property of a Hilbert-space-valued state. $)
    hstles $p |- ( ( ( S e. CHStates /\ A e. CH ) /\ ( B e. CH /\ A C_ B ) )
             -> ( ( normh ` ( S ` A ) ) = 1 -> ( normh ` ( S ` B ) ) = 1 ) ) $=
      ( chst wcel cch wa wss cfv cno c1 wceq cle wbr simpr hstle adantr
      eqbrtrrd ex hstle1 ad2ant2r jctild wb chil cr hstcl normcl 1re letri3
      mpan2 3syl sylibrd ) CDEZAFEZGBFEZABHZGGZACIJIZKLZBCIZJIZKMNZKVAMNZGZVAKL
      ZUQUSVCVBUQUSVCUQUSGURKVAMUQUSOUQURVAMNUSABCPQRSUMUOVBUNUPBCTUAUBUMUOVEVD
      UCZUNUPUMUOGUTUDEVAUEEZVFBCUFUTUGVGKUEEVFUHVAKUIUJUKUAUL $.
      $( [30-Jun-2006] $)

    $( A Hilbert-space-valued state orthogonal to the state of the lattice unit
       is zero. $)
    hstoh $p |- ( ( S e. CHStates /\ A e. CH /\ ( ( S ` A ) .ih ( S ` ~H ) ) =
                  0 ) -> ( S ` A ) = 0h ) $=
      ( chst wcel cch cfv chil csp co cc0 wceq w3a cno c0v c2 cexp wa cort cva
      caddc hstcl choccl sylan2 his7 syl111anc normsq syl eqcomd wss ococ
      eqimss2 jca adantl hstorth mpdan opreq12d cc cr normcl resqcl recnd
      addid1 3eqtrrd hstoc opreq2d eqtrd id sylan9eq 3impa wb sqeq0 3adant3
      mpbid hst0h ) BCDZAEDZABFZGBFZHIZJKZLZVQMFZJKZVQNKZWAWBOPIZJKZWCVOVPVTWFV
      OVPQZVTWEVSJWGWEVQVQARFZBFZSIZHIZVSWGWKVQVQHIZVQWIHIZTIZWEJTIZWEWGVQGDZWP
      WIGDZWKWNKABUAZWRVPVOWHEDZWQAUBZWHBUAUCVQVQWIUDUEWGWLWEWMJTWGWEWLWGWPWEWL
      KWRVQUFUGUHWGWSAWHRFZUIZQZWMJKVPXCVOVPWSXBWTVPXAAKXBAUJAXAUKUGULUMAWHBUNU
      OUPWGWEUQDWOWEKWGWEWGWBURDZWEURDWGWPXDWRVQUSUGZWBUTUGVAWEVBUGVCWGWJVRVQHA
      BVDVEVFVTVGVHVIVOVPWFWCVJZVTWGWBUQDXFWGWBXEVAWBVKUGVLVMVOVPWCWDVJVTABVNVL
      VM $.
      $( [25-Jun-2006] $)

    $( A Hilbert-space-valued state is zero at the zero subspace. $)
    hst0 $p |- ( S e. CHStates -> ( S ` 0H ) = 0h ) $=
      ( chst wcel c0h cfv chil csp co cc0 wceq c0v cch h0elch wa cort wss
      helch ssid occli ch0lei hstorth mpanr12 mpan2 hstoh mp3an2 mpdan ) ABCZDA
      EZFAEGHIJZUHKJZUGDLCZUIMUGUKNFLCDFOEZPUIQULFFRSTDFAUAUBUCUGUKUIUJMDAUDUEU
      F $.
      $( [30-Jun-2006] $)

    $( The value of a state at the full Hilbert space. $)
    sthil $p |- ( S e. States -> ( S ` ~H ) = 1 ) $=
      ( vx vy cst wcel cch cc0 c1 cicc co wf chil cfv wceq cv cort wss chj
      caddc wi wral isst simp2bi ) ADEFGHIJAKLAMHNBOZCOZPMQUDUERJAMUDAMUEAMSJNT
      CFUABFUABCAUBUC $.
      $( [23-Dec-2013] $) $( [23-Oct-1999] $)

    $( The value of a state on a join. $)
    stj $p |- ( S e. States ->
         ( ( ( A e. CH /\ B e. CH ) /\ A C_ ( _|_ ` B ) ) ->
         ( S ` ( A vH B ) ) = ( ( S ` A ) + ( S ` B ) ) ) ) $=
      ( vx vy cst wcel cch wa cort cfv wss chj co caddc wceq cv wi wral cc0 c1
      cicc wf chil isst simp3bi sseq1 opreq1 fveq2d fveq2 opreq1d eqeq12d
      imbi12d sseq2d opreq2 opreq2d rcla42v syl5com imp3a ) CFGZAHGBHGIZABJKZLZ
      ABMNZCKZACKZBCKZONZPZUTDQZEQZJKZLZVJVKMNZCKZVJCKZVKCKZONZPZRZEHSDHSZVAVCV
      IRZUTHTUAUBNCUCUDCKUAPWADECUEUFVTWBAVLLZAVKMNZCKZVFVQONZPZRDEABHHVJAPZVMW
      CVSWGVJAVLUGWHVOWEVRWFWHVNWDCVJAVKMUHUIWHVPVFVQOVJACUJUKULUMVKBPZWCVCWGVI
      WIVLVBAVKBJUJUNWIWEVEWFVHWIWDVDCVKBAMUOUIWIVQVGVFOVKBCUJUPULUMUQURUS $.
      $( [23-Dec-2013] $) $( [23-Oct-1999] $)
  $}

  ${
    sto1.1 $e |- A e. CH $.
    $( The state of a subspace plus the state of its orthocomplement. $)
    sto1i $p |- ( S e. States -> ( ( S ` A ) + ( S ` ( _|_ ` A ) ) ) = 1 ) $=
      ( cst wcel cort cfv chj co caddc c1 cch wa wss wceq choccli pm3.2i csh
      chshii shococss ax-mp stj mp2ani chil chjoi fveq2i sthil syl5eq eqtr3d )
      BDEZAAFGZHIZBGZABGUKBGJIZKUJALEZUKLEZMAUKFGNZUMUNOUOUPCACPQAREUQACSATUAAU
      KBUBUCUJUMUDBGKULUDBACUEUFBUGUHUI $.
      $( [24-Oct-1999] $)

    $( The state of the orthocomplement. $)
    sto2i $p |- ( S e. States ->
               ( S ` ( _|_ ` A ) ) = ( 1 - ( S ` A ) ) ) $=
      ( cst wcel c1 cfv cmin co cort wceq caddc sto1i cc wb cch cr stcl mpi
      recnd choccli ax-1cn subadd mp3an1 syl11anc mpbird eqcomd ) BDEZFABGZHIZA
      JGZBGZUHUJULKZUIULLIFKZABCMUHUINEZULNEZUMUNOZUHUIUHAPEUIQECABRSTUHULUHUKP
      EULQEACUAUKBRSTFNEUOUPUQUBFUIULUCUDUEUFUG $.
      $( [24-Oct-1999] $)

    $( If a state is greater than or equal to 1, it is 1. $)
    stge1i $p |- ( S e. States ->
                ( 1 <_ ( S ` A ) <-> ( S ` A ) = 1 ) ) $=
      ( cst wcel c1 cfv cle wbr wceq wa cch stle1 mpi anim1i ex cr wb stcl 1re
      jctir letri3 syl sylibrd leidi breq2 mpbiri impbid1 ) BDEZFABGZHIZUJFJZUI
      UKUJFHIZUKKZULUIUKUNUIUMUKUIALEZUMCABMNOPUIUJQEZFQEZKULUNRUIUPUQUIUOUPCAB
      SNTUAUJFUBUCUDULUKFFHIFTUEUJFFHUFUGUH $.
      $( [11-Nov-1999] $)

    $( If a state is less than or equal to 0, it is 0. $)
    stle0i $p |- ( S e. States ->
                ( ( S ` A ) <_ 0 <-> ( S ` A ) = 0 ) ) $=
      ( cst wcel cfv cc0 cle wbr wceq wa cch stge0 mpi anim2i expcom cr wb
      stcl 0re jctir letri3 syl sylibrd leidi breq1 mpbiri impbid1 ) BDEZABFZGH
      IZUJGJZUIUKUKGUJHIZKZULUKUIUNUIUMUKUIALEZUMCABMNOPUIUJQEZGQEZKULUNRUIUPUQ
      UIUOUPCABSNTUAUJGUBUCUDULUKGGHIGTUEUJGGHUFUGUH $.
      $( [11-Nov-1999] $)
  $}

  ${
    stle.1 $e |- A e. CH $.
    stle.2 $e |- B e. CH $.
    $( Ordering law for states. $)
    stlei $p |- ( S e. States -> ( A C_ B -> ( S ` A ) <_ ( S ` B ) ) ) $=
      ( cst wcel wss cfv cle wbr wa cort caddc co chj wceq cch csh chshii
      shococss ax-mp sstr2 mpi choccli pm3.2i jctil stj syl5 imp c1 chjcli
      stle1 sto1i breqtrrd adantr eqbrtrrd cr w3a wb stcl 3jca leadd1 syl
      mpbird ex ) CFGZABHZACIZBCIZJKZVGVHLZVKVIBMIZCIZNOZVJVNNOZJKZVLAVMPOZCIZV
      OVPJVGVHVSVOQZVHARGZVMRGZLZAVMMIZHZLVGVTVHWEWCVHBWDHZWEBSGWFBETBUAUBABWDU
      CUDWAWBDBEUEZUFUGAVMCUHUIUJVGVSVPJKVHVGVSUKVPJVGVRRGVSUKJKAVMDWGULVRCUMUD
      BCEUNUOUPUQVLVIURGZVJURGZVNURGZUSZVKVQUTVGWKVHVGWHWIWJVGWAWHDACVAUDVGBRGW
      IEBCVAUDVGWBWJWGVMCVAUDVBUPVIVJVNVCVDVEVF $.
      $( [24-Oct-1999] $)

    $( Ordering law for states. $)
    stlesi $p |- ( S e. States ->
                ( A C_ B -> ( ( S ` A ) = 1 -> ( S ` B ) = 1 ) ) ) $=
      ( cst wcel wss cfv c1 wceq wa cle wbr cr wb cch stcl mpi 1re jctir
      adantr letri3 syl stle1 stlei imp adantrr breq1 ad2antll mpbid mpbir2and
      exp32 ) CFGZABHZACIZJKZBCIZJKZUNUOUQLZLZUSURJMNZJURMNZVAUROGZJOGZLZUSVBVC
      LPUNVFUTUNVDVEUNBQGZVDEBCRSTUAUBURJUCUDUNVBUTUNVGVBEBCUESUBVAUPURMNZVCUNU
      OVHUQUNUOVHABCDEUFUGUHUQVHVCPUNUOUPJURMUIUJUKULUM $.
      $( [24-Oct-1999] $)

    $( Join of components of Sasaki arrow ->1. $)
    stji1i $p |- ( S e. States -> ( S ` ( ( _|_ ` A ) vH ( A i^i B ) ) ) =
         ( ( S ` ( _|_ ` A ) ) + ( S ` ( A i^i B ) ) ) ) $=
      ( cst wcel cort cfv cch cin wa wss chj co caddc wceq choccli chincli
      pm3.2i inss1 chsscon3i mpbi stj mp2ani ) CFGAHIZJGZABKZJGZLUFUHHIMZUFUHNO
      CIUFCIUHCIPOQUGUIADRABDESZTUHAMUJABUAUHAUKDUBUCUFUHCUDUE $.
      $( [24-Oct-1999] $)

    $( State of component of unit meet. $)
    stm1i $p |- ( S e. States ->
               ( ( S ` ( A i^i B ) ) = 1 -> ( S ` A ) = 1 ) ) $=
      ( cst wcel cin cfv c1 wceq cle wbr wss inss1 chincli stlei mpi breq1
      syl5ibcom stge1i sylibd ) CFGZABHZCIZJKZJACIZLMZUGJKUCUEUGLMZUFUHUCUDANUI
      ABOUDACABDEPDQRUEJUGLSTACDUAUB $.
      $( [11-Nov-1999] $)

    $( State of component of unit meet. $)
    stm1ri $p |- ( S e. States ->
               ( ( S ` ( A i^i B ) ) = 1 -> ( S ` B ) = 1 ) ) $=
      ( cin cfv c1 wceq cst wcel incom fveq2i eqeq1i stm1i syl5bi ) ABFZCGZHIBA
      FZCGZHICJKBCGHIRTHQSCABLMNBACEDOP $.
      $( [11-Nov-1999] $)

    $( Sum of states whose meet is 1. $)
    stm1addi $p |- ( S e. States -> ( ( S ` ( A i^i B ) ) = 1 ->
                  ( ( S ` A ) + ( S ` B ) ) = 2 ) ) $=
      ( cst wcel cin cfv c1 wceq wa caddc co c2 stm1i stm1ri jcad opreq12 df-2
      syl6eqr syl6 ) CFGZABHCIJKZACIZJKZBCIZJKZLZUEUGMNZOKUCUDUFUHABCDEPABCDEQR
      UIUJJJMNOUEJUGJMSTUAUB $.
      $( [11-Nov-1999] $)

    $( If the sum of 2 states is 2, then each state is 1. $)
    staddi $p |- ( S e. States -> ( ( ( S ` A ) + ( S ` B ) ) = 2 ->
                ( S ` A ) = 1 ) ) $=
      ( cst wcel cfv caddc co c2 wceq clt wbr wn c1 wne cr cch stcl mpi
      readdcl syl11anc wa 2re ltne mp3an2 necomd sylan ex necon2bd cle stle1
      wb 1re a1i leadd2 syl111anc mpbid adantr wi w3a ltadd1 biimpd imp
      sylancl readdcli lelttr mp2and df-2 syl6breqr con3d wo leloe ord 3syld )
      CFGZACHZBCHZIJZKLVTKMNZOVRPMNZOVRPLZVQWAVTKVQWAVTKQZVQVTRGZWAWDVQVRRGZVSR
      GZWEVQASGZWFDACTUAZVQBSGZWGEBCTUAZVRVSUBUCZWEWAUDKVTWEKRGWAKVTQUEVTKUFUGU
      HUIUJUKVQWBWAVQWBWAVQWBUDZVTPPIJZKMWMVTVRPIJZULNZWOWNMNZVTWNMNZVQWPWBVQVS
      PULNZWPVQWJWSEBCUMUAVQWGPRGZWFWSWPUNWKWTVQUOUPZWIVSPVRUQURUSUTVQWBWQVQWFW
      TWTWBWQVAWIXAXAWFWTWTVBWBWQVRPPVCVDURVEVQWPWQUDWRVAZWBVQWEWORGZWNRGZXBWLV
      QWFWTXCWIUOVRPUBVFXDVQPPUOUOVGUPVTWOWNVHURUTVIVJVKUJVLVQWBWCVQVRPULNZWBWC
      VMZVQWHXEDACUMUAVQWFWTXEXFUNWIUOVRPVNVFUSVOVP $.
      $( [12-Nov-1999] $)

    ${
      stm1add3.3 $e |- C e. CH $.
      $( Sum of states whose meet is 1. $)
      stm1add3i $p |- ( S e. States -> ( ( S ` ( ( A i^i B ) i^i C ) ) = 1 ->
                  ( ( ( S ` A ) + ( S ` B ) ) + ( S ` C ) ) = 3 ) ) $=
        ( cst wcel cin cfv c1 wceq caddc co c2 wa c3 chincli stm1i stm1addi
        syld stm1ri jcad opreq12 df-3 syl6eqr syl6 ) DHIZABJZCJDKLMZADKBDKNOZPM
        ZCDKZLMZQZULUNNOZRMUIUKUMUOUIUKUJDKLMUMUJCDABEFSZGTABDEFUAUBUJCDURGUCUD
        UPUQPLNORULPUNLNUEUFUGUH $.
        $( [11-Nov-1999] $)

      $( MAY BE REVISED: using new ltne , an extra eqcom was needed.  Any way
         to avoid this? $)
      $( If the sum of 3 states is 3, then each state is 1. $)
      stadd3i $p |- ( S e. States ->
  ( ( ( ( S ` A ) + ( S ` B ) ) + ( S ` C ) ) = 3 -> ( S ` A ) = 1 ) ) $=
        ( cst wcel cfv caddc co c3 wceq c1 cc cch cr stcl mpi recnd addass
        syl111anc eqeq1d clt wbr wn eqcom wne wi 3re readdcl syl11anc ltne
        3exp syl necon2bd syl5bi wa cle 1re jctir readdcli a1i stle1 wb leadd2
        mpbid leadd1 letrd adantr w3a ltadd1 biimpd imp lelttr mp2and c2 df-3
        df-2 opreq1i ax-1cn addassi 3eqtrri syl6breq ex con3d wo leloe ord
        3syld sylbid ) DHIZADJZBDJZKLCDJZKLZMNWNWOWPKLZKLZMNZWNONZWMWQWSMWMWNPI
        WOPIWPPIWQWSNWMWNWMAQIZWNRIZEADSTZUAWMWOWMBQIZWORIZFBDSTZUAWMWPWMCQIZWP
        RIZGCDSTZUAWNWOWPUBUCUDWMWTWSMUEUFZUGZWNOUEUFZUGXAWTMWSNWMXLWSMUHWMXKMW
        SWMMRIZXKMWSUIZUJZUKWMWSRIZXNXPUJWMXCWRRIZXQXDWMXFXIXRXGXJWOWPULUMZWNWR
        ULUMZXQXNXKXOWSMUNUOUPTUQURWMXMXKWMXMXKWMXMUSZWSOOOKLZKLZMUEYAWSWNYBKLZ
        UTUFZYDYCUEUFZWSYCUEUFZWMYEXMWMWRYBUTUFZYEWMWRWOOKLZYBXSWMXFORIZUSYIRIW
        MXFYJXGVAVBWOOULUPYBRIZWMOOVAVAVCZVDZWMWPOUTUFZWRYIUTUFZWMXHYNGCDVETWMX
        IYJXFYNYOVFXJYJWMVAVDZXGWPOWOVGUCVHWMWOOUTUFZYIYBUTUFZWMXEYQFBDVETWMXFY
        JYJYQYRVFXGYPYPWOOOVIUCVHVJWMXRYKXCYHYEVFXSYMXDWRYBWNVGUCVHVKWMXMYFWMXC
        YJYKXMYFUJXDYPYMXCYJYKVLXMYFWNOYBVMVNUCVOWMYEYFUSYGUJZXMWMXQYDRIZYCRIZY
        SXTWMXCYKUSYTWMXCYKXDYLVBWNYBULUPUUAWMOYBVAYLVCVDWSYDYCVPUCVKVQMVROKLYB
        OKLYCVSVRYBOKVTWAOOOWBWBWBWCWDWEWFWGWMXMXAWMWNOUTUFZXMXAWHZWMXBUUBEADVE
        TWMXCYJUSUUBUUCVFWMXCYJXDVAVBWNOWIUPVHWJWKWL $.
        $( [13-Nov-1999] $)
    $}
  $}

  $( The state of the zero subspace. $)
  st0 $p |- ( S e. States -> ( S ` 0H ) = 0 ) $=
    ( cst wcel chil cort cfv c1 cmin co c0h cc0 helch sto2i sthil opreq2d
    eqtrd choc1 fveq2i ax-1cn subidi 3eqtr3g ) ABCZDEFZAFZGGHIZJAFKUBUDGDAFZHIU
    EDALMUBUFGGHANOPUCJAQRGSTUA $.
    $( [24-Oct-1999] $)

  ${
    $d u x A $.  $d u x B $.
    strlem1.1 $e |- A e. CH $.
    strlem1.2 $e |- B e. CH $.
    $( Lemma for strong state theorem: if closed subspace ` A ` is not
       contained in ` B ` , there is a unit vector ` u ` in their
       difference. $)
    strlem1 $p |- ( -. A C_ B -> E. u e. ( A \ B ) ( normh ` u ) = 1 ) $=
      ( vx wss wn cv cdif wcel wex cno cfv c1 wceq wrex c0 ssdif0 notbii neq0
      bitri cdiv co csm wa cc wi cr cc0 wne chil eldifi cheli normcl 3syl c0v
      cch ch0 ax-mp eldifn mt2 eleq1 mtbiri con2i wb norm-i mtbird df-ne
      sylibr rereccl syl11anc recnd csh chshii shmulclOLD ex syl shmulcl
      mp3an1 cmul recid opreq1d ax-hvmulass syl111anc ax-hvmulid 3eqtr3d
      eleq1d sylibd con3d anim12d eldif 3imtr4g pm2.43i cabs norm-iii cle wbr
      clt necon2ai normgt0 mpbid 1re 0re lt01 ltleii divge0 mpanl12 absid
      mulcom eqtrd 3eqtrd fveq2 eqeq1d rcla4ev 19.23aiv sylbi ) BCGZHZFIZBCJZKZ
      FLZAIZMNZOPZAYAQZXSYARPZHYCXRYHBCSTFYAUAUBYBYGFYBOXTMNZUCUDZXTUEUDZYAKZYK
      MNZOPZYGYBYLYBXTBKZXTCKZHZUFYKBKZYKCKZHZUFYBYLYBYOYRYQYTYBYJUGKZYOYRUHYBY
      JYBYIUIKZYIUJUKZYJUIKZYBYOXTULKZUUBXTBCUMZXTBDUNZXTUOUPZYBYIUJPZHUUCYBUUI
      XTUQPZUUJYBUUJYBUQYAKZUUKUQCKZCURKUULECUSUTUQBCVAVBXTUQYAVCVDZVEYBYOUUEUU
      IUUJVFUUFUUGXTVGUPVHYIUJVIVJZYIVKVLZVMZUUAYOYRBVNKUUAYOUFYRUHBDVOYJXTBVPU
      TVQVRYBYSYPYBYSYIYKUEUDZCKZYPYBYIUGKZYSUURUHYBYIUUHVMZUUSYSUURCVNKUUSYSUU
      RCEVOYIYKCVSVTVQVRYBUUQXTCYBYIYJWAUDZXTUEUDZOXTUEUDZUUQXTYBUVAOXTUEYBUUSU
      UCUVAOPUUTUUNYIWBVLZWCYBUUSUUAUUEUVBUUQPUUTUUPYBYOUUEUUFUUGVRZYIYJXTWDWEY
      BYOUUEUVCXTPUUFUUGXTWFUPWGWHWIWJWKXTBCWLYKBCWLWMWNYBYMYJWONZYIWAUDZYJYIWA
      UDZOYBUUAUUEYMUVGPUUPUVEYJXTWPVLYBUVFYJYIWAYBUUDUJYJWQWRZUVFYJPUUOYBUUBUJ
      YIWSWRZUVIUUHYBXTUQUKZUVJYBXTUQUUMWTYBYOUUEUVKUVJVFUUFUUGXTXAUPXBOUIKUJOW
      QWRUUBUVJUFUVIXCUJOXDXCXEXFOYIXGXHVLYJXIVLWCYBUVHUVAOYBUUAUUSUVHUVAPUUPUU
      TYJYIXJVLUVDXKXLYFYNAYKYAYDYKPYEYMOYDYKMXMXNXOVLXPXQ $.
      $( [25-Oct-1999] $)
  $}

  ${
    $d x y C $.  $d x y u $.
    strlem2.1 $e |- S = { <. x , y >. | ( x e. CH /\
                    y = ( ( normh ` ( ( proj ` x ) ` u ) ) ^ 2 ) ) } $.
    $( Lemma for strong state theorem. $)
    strlem2 $p |- ( C e. CH ->
                  ( S ` C ) = ( ( normh ` ( ( proj ` C ) ` u ) ) ^ 2 ) ) $=
      ( cv cpj cfv cno c2 cexp co cch wceq fveq2 fveq1d fveq2d opreq1d oprex
      fvopab4 ) ABDCGZAGZHIZIZJIZKLMUBDHIZIZJIZKLMNEUCDOZUFUIKLUJUEUHJUJUBUDUGU
      CDHPQRSFUIKLTUA $.
      $( [28-Oct-1999] $)
  $}

  ${
    $d x y z w u $.  $d z w S $.
    strlem3a.1 $e |- S = { <. x , y >. | ( x e. CH /\
                    y = ( ( normh ` ( ( proj ` x ) ` u ) ) ^ 2 ) ) } $.
    $( Lemma for strong state theorem: the function ` S ` , that maps a closed
       subspace to the square of the norm of its projection onto a unit vector,
       is a state. $)
    strlem3a $p |- ( ( u e. ~H /\ ( normh ` u ) = 1 ) -> S e. States ) $=
      ( vz vw cv chil wcel cno cfv c1 wceq wa cch cc0 cicc co wf cort wss chj
      caddc wi wral w3a cst cpj c2 cexp cr cle wbr id simpl pjhcl syl2anc
      normcl syl resqcl sqge0 normge0 pjnorm simplr breqtrd cn0 2nn0 exple1
      mpan2 syl111anc 3jca wb 0re 1re elicc2 mp2an sylibr r19.21aiva fopab2
      sylib helch strlem2 ax-mp pjch1 fveq2d opreq1d opreq1 sq1 syl6eq
      sylan9eq syl5eq cva pjcjt2 imp pjopyth eqtrd chjcl 3adant3 adantr 3simpa
      opreqan12d 3eqtr4d 3exp1 com3r r19.21adv r19.21aiv isst ) CHZIJZXIKLZMNZO
      ZPQMRSZDTZIDLZMNZFHZGHZUALUBZXRXSUCSZDLZXRDLZXSDLZUDSZNZUEZGPUFZFPUFZUGDU
      HJXMXOXQYIXMXIAHZUILLZKLZUJUKSZXNJZAPUFXOXMYNAPXMYJPJZOZYMULJZQYMUMUNZYMM
      UMUNZUGZYNYPYQYRYSYPYLULJZYQYPYKIJZUUAYOYOXJUUBXMYOUOZXJXLUPZXIYJUQURZYKU
      SUTZYLVAUTYPUUAYRUUFYLVBUTYPUUAQYLUMUNZYLMUMUNZYSUUFYPUUBUUGUUEYKVCUTYPYL
      XKMUMYOYOXJYLXKUMUNXMUUCUUDXIYJVDURXJXLYOVEVFUUAUUGUUHUGUJVGJYSVHYLUJVIVJ
      VKVLQULJMULJYNYTVMVNVOQMYMVPVQVRVSABPXNYMDEVTWAXMXPXIIUILLZKLZUJUKSZMIPJX
      PUUKNWBABCIDEWCWDXJXLUUKXKUJUKSZMXJUUJXKUJUKXJUUIXIKXIWEWFWGXLUULMUJUKSMX
      KMUJUKWHWIWJWKWLXMYHFPXMXRPJZYGGPXJUUMXSPJZYGUEUEXLUUMUUNXJYGUUMUUNXJXTYF
      UUMUUNXJUGZXTOZXIYAUILLZKLZUJUKSZXIXRUILLZKLUJUKSZXIXSUILLZKLUJUKSZUDSZYB
      YEUUPUUSUUTUVBWMSZKLZUJUKSZUVDUUPUURUVFUJUKUUPUUQUVEKUUOXTUUQUVENXIXSXRWN
      WOWFWGUUOXTUVGUVDNXIXSXRWPWOWQUUPYAPJZYBUUSNUUOUVHXTUUMUUNUVHXJXRXSWRWSWT
      ABCYADEWCUTUUPUUMUUNOZYEUVDNUUOUVIXTUUMUUNXJXAWTUUMUUNYCUVAYDUVCUDABCXRDE
      WCABCXSDEWCXBUTXCXDXEWTXFXGVLFGDXHVR $.
      $( [23-Dec-2013] $) $( [28-Oct-1999] $)
  $}

  ${
    $d x z w ph $.  $d z w S $.  $d x y u $.  $d y z w $.  $d x y A $.
    $d x y B $.
    strlem3.1 $e |- S = { <. x , y >. | ( x e. CH /\
                    y = ( ( normh ` ( ( proj ` x ) ` u ) ) ^ 2 ) ) } $.
    strlem3.2 $e |- ( ph <-> ( u e. ( A \ B ) /\ ( normh ` u ) = 1 ) ) $.
    strlem3.3 $e |- A e. CH $.
    strlem3.4 $e |- B e. CH $.
    $( Lemma for strong state theorem: the function ` S ` , that maps a closed
       subspace to the square of the norm of its projection onto a unit vector,
       is a state.  This lemma restates the hypotheses in a more convenient
       form to work with. $)
    strlem3 $p |- ( ph -> S e. States ) $=
      ( cv cdif wcel cno cfv c1 wceq wa cst chil eldifi cheli syl strlem3a
      sylan sylbi ) ADLZEFMNZUHOPQRZSGTNZIUIUHUANZUJUKUIUHENULUHEFUBUHEJUCUDBCD
      GHUEUFUG $.
      $( [28-Oct-1999] $)

    $( Lemma for strong state theorem. $)
    strlem4 $p |- ( ph -> ( S ` A ) = 1 ) $=
      ( cfv cv cpj cno c2 cexp co c1 cch wcel wceq strlem2 ax-mp cdif wa
      eldifi pjid mpan fveq2d eqeq2 syl5ib impcom sylan sylbi opreq1d sq1
      syl6eq syl5eq ) AEGLZDMZENLLZOLZPQRZSETUAZUTVDUBJBCDEGHUCUDAVDSPQRSAVCSPQ
      AVAEFUEUAZVAOLZSUBZUFVCSUBZIVFVAEUAZVHVIVAEFUGVHVJVIVJVCVGUBVHVIVJVBVAOVE
      VJVBVAUBJVAEUHUIUJVGSVCUKULUMUNUOUPUQURUS $.
      $( [2-Nov-1999] $)

    $( Lemma for strong state theorem. $)
    strlem5 $p |- ( ph -> ( S ` B ) < 1 ) $=
      ( cv cdif wcel cno cfv c1 wceq wa clt wbr c2 cexp co cpj cch strlem2
      ax-mp wn eldif chil cheli wb pjnel mpan biimpa sylan sylbi breq2 syl5ib
      impcom eldifi cr cc0 cle pjhcli normcl syl normge0 1re 0re lt01 ltleii
      lt2sq mpanr12 syl11anc 3syl adantr mpbid syl5eqbr sq1 syl6breq ) ADLZEFMN
      ZWCOPZQRZSZFGPZQTUAIWGWHQUBUCUDZQTWGWHWCFUEPPZOPZUBUCUDZWITFUFNZWHWLRKBCD
      FGHUGUHWGWKQTUAZWLWITUAZWFWDWNWDWKWETUAZWFWNWDWCENZWCFNUIZSWPWCEFUJWQWCUK
      NZWRWPWCEJULZWSWRWPWMWSWRWPUMKWCFUNUOUPUQURWEQWKTUSUTVAWDWNWOUMZWFWDWQWSX
      AWCEFVBWTWSWKVCNZVDWKVEUAZXAWSWJUKNZXBWCFKVFZWJVGVHWSXDXCXEWJVIVHXBXCSQVC
      NVDQVEUAXAVJVDQVKVJVLVMWKQVNVOVPVQVRVSVTWAWBUR $.
      $( [2-Nov-1999] $)

    $( Lemma for strong state theorem. $)
    strlem6 $p |- ( ph -> -. ( ( S ` A ) = 1 -> ( S ` B ) = 1 ) ) $=
      ( cfv c1 wceq wn wa wi strlem4 wne cr wcel clt wbr cst strlem3 cch stcl
      mpi syl strlem5 1re ltne mp3an2 syl11anc necomd df-ne sylib jca annim )
      AEGLMNZFGLZMNZOZPUTVBQOAUTVCABCDEFGHIJKRAVAMSVCAMVAAVATUAZVAMUBUCZMVASZAG
      UDUAZVDABCDEFGHIJKUEVGFUFUAVDKFGUGUHUIABCDEFGHIJKUJVDMTUAVEVFUKVAMULUMUNU
      OVAMUPUQURUTVBUSUQ $.
      $( [2-Nov-1999] $)
  $}

  ${
    $d x y u f A $.  $d x y u f B $.
    str.1 $e |- A e. CH $.
    str.2 $e |- B e. CH $.
    $( Strong state theorem.  The states on a Hilbert lattice define an
       ordering.  Remark in [Mayet] p. 370. $)
    stri $p |- ( A. f e. States ( ( f ` A ) = 1 -> ( f ` B ) = 1 ) ->
              A C_ B ) $=
      ( vu vx vy cv cfv c1 wceq wi cst wral wn wrex wss dfral2 cno cdif
      strlem1 wcel wa cch cpj c2 cexp co copab eqid biid strlem3 strlem6 fveq1
      eqeq1d imbi12d notbid rcla4ev syl11anc r19.23aiva syl con1i sylbi ) ACIZJ
      ZKLZBVEJZKLZMZCNOVJPZCNQZPABRZVJCNSVMVLVMPFIZTJKLZFABUAZQVLFABDEUBVOVLFVP
      VNVPUCVOUDZGIZUEUCHIVNVRUFJJTJUGUHUILUDGHUJZNUCAVSJZKLZBVSJZKLZMZPZVLVQGH
      FABVSVSUKZVQULZDEUMVQGHFABVSWFWGDEUNVKWECVSNVEVSLZVJWDWHVGWAVIWCWHVFVTKAV
      EVSUOUPWHVHWBKBVEVSUOUPUQURUSUTVAVBVCVD $.
      $( [2-Nov-1999] $)

    $( Strong state theorem (bidirectional version). $)
    strb $p |- ( A. f e. States ( ( f ` A ) = 1 -> ( f ` B ) = 1 ) <->
              A C_ B ) $=
      ( cv cfv c1 wceq wi cst wral wss stri wcel stlesi com12 r19.21aiv impbii
      ) ACFZGHIBTGHIJZCKLABMZABCDENUBUACKTKOUBUAABTDEPQRS $.
      $( [7-Apr-2001] $)
  $}

  ${
    $d x y C $.  $d x y u $.
    hstrlem2.1 $e |- S = { <. x , y >. | ( x e. CH /\
                      y = ( ( proj ` x ) ` u ) ) } $.
    $( Lemma for strong set of CH states theorem. $)
    hstrlem2 $p |- ( C e. CH ->
                  ( S ` C ) = ( ( proj ` C ) ` u ) ) $=
      ( cv cpj cfv cch wceq fveq2 fveq1d fvex fvopab4 ) ABDCGZAGZHIZIPDHIZIJEQD
      KPRSQDHLMFPSNO $.
      $( [30-Jun-2006] $)
  $}

  ${
    $d x z w u $.  $d z w S $.  $d x y u $.  $d y z w $.
    hstrlem3a.1 $e |- S = { <. x , y >. | ( x e. CH /\
                      y = ( ( proj ` x ) ` u ) ) } $.
    $( Lemma for strong set of CH states theorem: the function ` S ` , that
       maps a closed subspace to the square of the norm of its projection onto
       a unit vector, is a state. $)
    hstrlem3a $p |- ( ( u e. ~H /\ ( normh ` u ) = 1 ) -> S e. CHStates ) $=
      ( vz vw cv chil wcel cno cfv c1 wceq wa cch wf cort wss csp co cc0 chj
      cva wi wral w3a chst cpj pjhcl adantrr expcom r19.21aiv fopab2 sylib
      helch hstrlem2 ax-mp fveq2i pjch1 fveq2d id sylan9eq syl5eq opreqan12d
      3adant3 adantr pjoi0 eqtrd pjcjt2 imp chjcl syl 3eqtr4d jca 3exp1 com3r
      r19.21adv 3jca ishst sylibr ) CHZIJZWBKLZMNZOZPIDQZIDLZKLZMNZFHZGHZRLSZWK
      DLZWLDLZTUAZUBNZWKWLUCUAZDLZWNWOUDUAZNZOZUEZGPUFZFPUFZUGDUHJWFWGWJXEWFWBA
      HZUILLZIJZAPUFWGWFXHAPXFPJZWFXHXIWCXHWEWBXFUJUKULUMABPIXGDEUNUOWFWIWBIUIL
      LZKLZMWHXJKIPJWHXJNUPABCIDEUQURUSWCWEXKWDMWCXJWBKWBUTVAWEVBVCVDWFXDFPWFWK
      PJZXCGPWCXLWLPJZXCUEUEWEXLXMWCXCXLXMWCWMXBXLXMWCUGZWMOZWQXAXOWPWBWKUILLZW
      BWLUILLZTUAZUBXNWPXRNZWMXLXMXSWCXLXMWNXPWOXQTABCWKDEUQZABCWLDEUQZVEVFVGWB
      WKWLVHVIXOWBWRUILLZXPXQUDUAZWSWTXNWMYBYCNWBWLWKVJVKXNWSYBNZWMXLXMYDWCXLXM
      OWRPJYDWKWLVLABCWRDEUQVMVFVGXNWTYCNZWMXLXMYEWCXLXMWNXPWOXQUDXTYAVEVFVGVNV
      OVPVQVGVRUMVSFGDVTWA $.
      $( [30-Jun-2006] $)
  $}

  ${
    $d x z w ph $.  $d z w S $.  $d x y u $.  $d y z w $.  $d x y A $.
    $d x y B $.
    hstrlem3.1 $e |- S = { <. x , y >. | ( x e. CH /\
                      y = ( ( proj ` x ) ` u ) ) } $.
    hstrlem3.2 $e |- ( ph <-> ( u e. ( A \ B ) /\ ( normh ` u ) = 1 ) ) $.
    hstrlem3.3 $e |- A e. CH $.
    hstrlem3.4 $e |- B e. CH $.
    $( Lemma for strong set of CH states theorem: the function ` S ` , that
       maps a closed subspace to the square of the norm of its projection onto
       a unit vector, is a state.  This lemma restates the hypotheses in a more
       convenient form to work with. $)
    hstrlem3 $p |- ( ph -> S e. CHStates ) $=
      ( cv cdif wcel cno cfv c1 wceq wa chst chil eldifi cheli syl hstrlem3a
      sylan sylbi ) ADLZEFMNZUHOPQRZSGTNZIUIUHUANZUJUKUIUHENULUHEFUBUHEJUCUDBCD
      GHUEUFUG $.
      $( [30-Jun-2006] $)

    $( Lemma for strong set of CH states theorem. $)
    hstrlem4 $p |- ( ph -> ( normh ` ( S ` A ) ) = 1 ) $=
      ( cfv cno cv cpj c1 cch wcel wceq hstrlem2 ax-mp fveq2i cdif wa eldifi
      pjid mpan fveq2d eqeq2 syl5ib impcom sylan sylbi syl5eq ) AEGLZMLDNZEOLLZ
      MLZPUOUQMEQRZUOUQSJBCDEGHTUAUBAUPEFUCRZUPMLZPSZUDURPSZIUTUPERZVBVCUPEFUEV
      BVDVCVDURVASVBVCVDUQUPMUSVDUQUPSJUPEUFUGUHVAPURUIUJUKULUMUN $.
      $( [30-Jun-2006] $)

    $( Lemma for strong set of CH states theorem. $)
    hstrlem5 $p |- ( ph -> ( normh ` ( S ` B ) ) < 1 ) $=
      ( cv cdif wcel cno cfv c1 wceq wa clt wbr cpj cch hstrlem2 fveq2d ax-mp
      wn eldif chil cheli wb pjnel mpan biimpa sylan sylbi breq2 syl5ib impcom
      syl5eqbr ) ADLZEFMNZVAOPZQRZSZFGPZOPZQTUAIVEVGVAFUBPPZOPZQTFUCNZVGVIRKVJV
      FVHOBCDFGHUDUEUFVDVBVIQTUAZVBVIVCTUAZVDVKVBVAENZVAFNUGZSVLVAEFUHVMVAUINZV
      NVLVAEJUJVOVNVLVJVOVNVLUKKVAFULUMUNUOUPVCQVITUQURUSUTUP $.
      $( [30-Jun-2006] $)

    $( Lemma for strong set of CH states theorem. $)
    hstrlem6 $p |- ( ph -> -. ( ( normh ` ( S ` A ) ) = 1 ->
                    ( normh ` ( S ` B ) ) = 1 ) ) $=
      ( cfv cno c1 wceq wn wa wi hstrlem4 wne cr wcel clt wbr chst chil
      hstrlem3 cch hstcl mpan2 normcl 3syl hstrlem5 1re ltne mp3an2 syl11anc
      necomd df-ne sylib jca annim ) AEGLMLNOZFGLZMLZNOZPZQVCVFRPAVCVGABCDEFGHI
      JKSAVENTVGANVEAVEUAUBZVENUCUDZNVETZAGUEUBZVDUFUBZVHABCDEFGHIJKUGVKFUHUBVL
      KFGUIUJVDUKULABCDEFGHIJKUMVHNUAUBVIVJUNVENUOUPUQURVENUSUTVAVCVFVBUT $.
      $( [30-Jun-2006] $)
  $}

  ${
    $d x y u f A $.  $d x y u f B $.
    hstr.1 $e |- A e. CH $.
    hstr.2 $e |- B e. CH $.
    $( Hilbert space admits a strong set of Hilbert-space-valued states
       (CH-states).  Theorem in [Mayet3] p. 10. $)
    hstri $p |- ( A. f e. CHStates ( ( normh ` ( f ` A ) ) = 1 ->
                ( normh ` ( f ` B ) ) = 1 ) -> A C_ B ) $=
      ( vu vx vy cv cfv cno c1 wceq wi chst wral wn wrex wss dfral2 cdif
      strlem1 wcel wa cch cpj copab eqid biid hstrlem3 hstrlem6 fveq1 fveq2d
      eqeq1d imbi12d notbid rcla4ev syl11anc r19.23aiva syl con1i sylbi ) ACIZJ
      ZKJZLMZBVCJZKJZLMZNZCOPVJQZCORZQABSZVJCOTVMVLVMQFIZKJLMZFABUAZRVLFABDEUBV
      OVLFVPVNVPUCVOUDZGIZUEUCHIVNVRUFJJMUDGHUGZOUCAVSJZKJZLMZBVSJZKJZLMZNZQZVL
      VQGHFABVSVSUHZVQUIZDEUJVQGHFABVSWHWIDEUKVKWGCVSOVCVSMZVJWFWJVFWBVIWEWJVEW
      ALWJVDVTKAVCVSULUMUNWJVHWDLWJVGWCKBVCVSULUMUNUOUPUQURUSUTVAVB $.
      $( [30-Jun-2006] $)

    $( Strong CH-state theorem (bidirectional version).  Theorem in [Mayet3]
       p. 10 and its converse. $)
    hstrbi $p |- ( A. f e. CHStates ( ( normh ` ( f ` A ) ) = 1 ->
              ( normh ` ( f ` B ) ) = 1 ) <-> A C_ B ) $=
      ( cv cfv cno c1 wceq wi chst wral wss hstri wcel cch wa hstles mpanr1
      mpanl2 expcom r19.21aiv impbii ) ACFZGHGIJBUEGHGIJKZCLMABNZABCDEOUGUFCLUE
      LPZUGUFUHAQPZUGUFDUHUIRBQPUGUFEABUESTUAUBUCUD $.
      $( [30-Jun-2006] $)
  $}

  ${
    $d f A $.
    large.1 $e |- A e. CH $.
    $( A Hilbert lattice admits a largei set of states.  Remark in [Mayet]
       p. 370. $)
    largei $p |- ( -. A = 0H <-> E. f e. States ( f ` A ) = 1 ) $=
      ( cv cfv c1 wceq cst wrex c0h wn wral ralnex wi wss wcel wb cc0 wne
      ax-1ne0 df-ne mpbi st0 eqeq1d eqcom syl6bb mtbiri mtt syl ralbiia h0elch
      strb chle0i 3bitri bitr3i con1bii ) ABDZEFGZBHIZAJGZUSKURKZBHLZUTURBHMVBU
      RJUQEZFGZNZBHLAJOUTVAVEBHUQHPZVDKVAVEQVFVDFRGZFRSVGKTFRUAUBVFVDRFGVGVFVCR
      FUQUCUDRFUEUFUGVDURUHUIUJAJBCUKULACUMUNUOUP $.
      $( [7-Apr-2001] $)
  $}

  ${
    jplem1.1 $e |- A e. CH $.
    $( Lemma for Jauch-Piron theorem. $)
    jplem1 $p |- ( ( u e. ~H /\ ( normh ` u ) = 1 ) ->
          ( u e. A <-> ( ( normh ` ( ( proj ` A ) ` u ) ) ^ 2 ) = 1 ) ) $=
      ( cv chil wcel cno cfv c1 wceq wa cpj c2 cexp co cch wb pjnorm2 mpan
      eqeq2 sylan9bb sq1 eqeq2i cr cc0 cle wbr pjhcli normcl syl normge0 1re
      0re lt01 ltleii sq11 mpanr12 syl11anc syl5bbr adantr bitr4d ) ADZEFZVBGHZ
      IJZKVBBFZVBBLHHZGHZIJZVHMNOZIJZVCVFVHVDJZVEVIBPFVCVFVLQCVBBRSVDIVHTUAVCVK
      VIQVEVKVJIMNOZJZVCVIVMIVJUBUCVCVHUDFZUEVHUFUGZVNVIQZVCVGEFZVOVBBCUHZVGUIU
      JVCVRVPVSVGUKUJVOVPKIUDFUEIUFUGVQULUEIUMULUNUOVHIUPUQURUSUTVA $.
      $( [31-Jul-2006] $) $( [8-Apr-2001] $)
  $}

  ${
    $d x z w u $.  $d z w S $.  $d x y u $.  $d y z w $.  $d x y A $.
    $d x y B $.
    jp.1 $e |- S = { <. x , y >. | ( x e. CH /\
                    y = ( ( normh ` ( ( proj ` x ) ` u ) ) ^ 2 ) ) } $.
    jp.2 $e |- A e. CH $.
    $( Lemma for Jauch-Piron theorem. $)
    jplem2 $p |- ( ( u e. ~H /\ ( normh ` u ) = 1 ) ->
                 ( u e. A <-> ( S ` A ) = 1 ) ) $=
      ( cv chil wcel cno cfv c1 wceq wa cpj c2 cexp co jplem1 cch strlem2
      ax-mp eqeq1i syl6bbr ) CHZIJUFKLMNOUFDJUFDPLLKLQRSZMNDELZMNCDGTUHUGMDUAJU
      HUGNGABCDEFUBUCUDUE $.
      $( [8-Apr-2001] $)

    ${
      jp.3 $e |- B e. CH $.
      $( The function ` S ` , that maps a closed subspace to the square of the
         norm of its projection onto a unit vector, is a Jauch-Piron state.
         Remark in [Mayet] p. 370.  (See ~ strlem3a for the proof that ` S ` is
         a state.) $)
      jpi $p |- ( ( u e. ~H /\ ( normh ` u ) = 1 ) -> ( ( ( S ` A ) = 1
               /\ ( S ` B ) = 1 ) <-> ( S ` ( A i^i B ) ) = 1 ) ) $=
        ( cv chil wcel cno cfv c1 wceq wa cin elin jplem2 anbi12d syl5bb
        chincli bitr3d ) CJZKLUEMNOPQZUEDERZLZDFNOPZEFNOPZQZUGFNOPUHUEDLZUEELZQ
        UFUKUEDESUFULUIUMUJABCDFGHTABCEFGITUAUBABCUGFGDEHIUCTUD $.
        $( [8-Apr-2001] $)
    $}
  $}

