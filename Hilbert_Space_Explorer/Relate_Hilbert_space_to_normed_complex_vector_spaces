$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Relate Hilbert space to normed complex vector spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Hilbert space vector addition is an Abelian group operation. $)
    hilabl $p |- +h e. AbelOp $=
      ( vx vy vz cva chil c0v c1 cv csm co ax-hilex ax-hfvadd ax-hvass ax-hv0cl
      cneg hvaddid2 cc wcel ax-1cn ax-hvcom negcli hvmulcl mpan mpancom hvnegid
      wceq eqtrd isgrpoi cxp fdmi isabloi ) ABDEABCFDGOZAHZIJZEKLUMBHZCHMNUMPUL
      QRUMERZUNERZGSUAULUMUBUCZUPUNUMDJZUMUNDJZFUQUPUSUTUFURUNUMTUDUMUEUGUHEEUI
      EDLUJUMUOTUK $.
      $( [10-Aug-2008] $) $( [15-Apr-2007] $)

    $( The group identity element of Hilbert space vector addition is the zero
       vector. $)
    hilid $p |- ( Id ` +h ) = 0h $=
      ( vy vx vz cva cgi cfv cv co wceq chil wral crio c0v cgr wcel cabl hilabl
      ablogrpo ax-mp cxp ax-hfvadd fdmi grprn eqid grpoidval hvaddid2 rgen wreu
      wb ax-hv0cl grpoideu ax-17 a17d opreq1 eqeq1d ralbidv riota2f mp2an eqtri
      mpbi ) DEFZAGZBGZDHZVCIZBJKZAJLZMDNOZVAVGIDPOVHQDRSZBAVADJDJVIJJTJDUAUBUC
      ZVAUDUESMVCDHZVCIZBJKZVGMIZVLBJVCUFUGMJOZVFAJUHZVMVNUIUJVHVPVIBADJVJUKSVF
      VMACJMCGMOAULVOVMAUMVBMIZVEVLBJVQVDVKVCVBMVCDUNUOUPUQURUTUS $.
      $( [25-Jul-2008] $) $( [16-Apr-2007] $)

    $( Hilbert space is a complex vector space.  Vector addition is ` +h ` ,
       and scalar product is ` .h ` . $)
    hilvc $p |- <. +h , .h >. e. CVec $=
      ( vx vy vz csm cva cop chil hilabl ax-hfvadd fdmi ax-hfvmul cv ax-hvmulid
      cxp ax-hvdistr1 ax-hvdistr2 ax-hvmulass eqid isvci ) ABCDEEDFZGHGGNGEIJKA
      LZMBLZUACLZOUBUCUAPUBUCUAQTRS $.
      $( [15-Apr-2007] $)
  $}

  ${
    $d x U $.
    hilnorm.5 $e |- ~H = ( BaseSet ` U ) $.
    hilnorm.2 $e |- .ih = ( .i ` U ) $.
    hilnorm.9 $e |- U e. NrmCVec $.
    $( Hilbert space norm in terms of vector space norm. $)
    hilnormi $p |- normh = ( norm ` U ) $=
      ( vx chil cv cnm cfv cmpt csp co csqr cno cnv wcel wceq eqid cr ax-mp wfn
      ipnm mpan mpteq2ia wf nvf ffn dffn5v mpbi dfhnorm2 3eqtr4ri ) EFEGZAHIZIZ
      JZEFULULKLMIZJUMNEFUNUPAOPZULFPUNUPQDULKAUMFBUMRZCUBUCUDUMFUAZUMUOQFSUMUE
      ZUSUQUTDAUMFBURUFTFSUMUGTEFUMUHUIEUJUK $.
      $( [23-Dec-2013] $) $( [11-Sep-2007] $)
  $}

  ${
    hilhh.1 $e |- ~H = ( BaseSet ` U ) $.
    hilhh.2 $e |- +h = ( +v ` U ) $.
    hilhh.3 $e |- .h = ( .s ` U ) $.
    hilhh.5 $e |- .ih = ( .i ` U ) $.
    hilhh.9 $e |- U e. NrmCVec $.
    $( Deduce the structure of Hilbert space from its components. $)
    hilhhi $p |- U = <. <. +h , .h >. , normh >. $=
      ( cnv wcel cva csm cop cno wceq hilnormi nvop ax-mp ) AGHAIJKLKMFJAILCDAB
      EFNOP $.
      $( [10-Apr-2008] $)
  $}

  ${
    $d k v w x y z U $.
    hhnv.1 $e |- U = <. <. +h , .h >. , normh >. $.
    $( Hilbert space is a normed complex vector space. $)
    hhnv $p |- U e. NrmCVec $=
      ( vx vy csm cva cno chil c0v cabl cgr hilabl ablogrpo ax-mp cxp ax-hfvadd
      wcel cfv cv wceq fdmi grprn cgi hilid eqcomi hilvc norm-i biimpa norm-iii
      normf cc0 norm-ii isnvi ) CDEAFGHIFHFJQFKQLFMNHHOHFPUAUBFUCRIUDUEUFUJCSZH
      QUNGRUKTUNITUNUGUHDSZUNUIUNUOULBUM $.
      $( [25-Jul-2008] $) $( [17-Nov-2007] $)

    $( The group (addition) operation of Hilbert space. $)
    hhva $p |- +h = ( +v ` U ) $=
      ( hhnv h2hva ) ABABCD $.
      $( [17-Nov-2007] $)

    $( The base set of Hilbert space.  This theorem provides an independent
       proof of ~ df-hba (see comments in that definition). $)
    hhba $p |- ~H = ( BaseSet ` U ) $=
      ( chil cva crn cba cfv cabl wcel cgr hilabl ablogrpo ax-mp ax-hfvadd fdmi
      cxp grprn eqid hhva bafval eqtr4i ) CDEAFGZDCDHIDJIKDLMCCPCDNOQADUBUBRABS
      TUA $.
      $( [25-Jul-2008] $) $( [17-Nov-2007] $)

    $( The zero vector of Hilbert space. $)
    hh0v $p |- 0h = ( 0vec ` U ) $=
      ( cn0v cfv cpv cgi cva wcel wceq hhnv eqid 0vfval ax-mp hhva fveq2i hilid
      c0v cnv 3eqtr2ri ) ACDZAEDZFDZGFDQARHTUBIABJAUARTUAKTKLMGUAFABNOPS $.
      $( [23-Dec-2013] $) $( [17-Nov-2007] $)

    $( The scalar product operation of Hilbert space. $)
    hhsm $p |- .h = ( .s ` U ) $=
      ( hhnv h2hsm ) ABABCD $.
      $( [17-Nov-2007] $)

    $( The vector subtraction operation of Hilbert space. $)
    hhvs $p |- -h = ( -v ` U ) $=
      ( hhnv hhba h2hvs ) ABABCABDE $.
      $( [13-Dec-2007] $)

    $( The norm function of Hilbert space. $)
    hhnm $p |- normh = ( norm ` U ) $=
      ( hhnv h2hnm ) ABABCD $.
      $( [17-Nov-2007] $)

    ${
      hhims.2 $e |- D = ( normh o. -h ) $.
      $( The induced metric of Hilbert space. $)
      hhims $p |- D = ( IndMet ` U ) $=
        ( cno cmv ccom cims cfv cnv wcel wceq hhnv hhvs hhnm eqid imsval eqtr4i
        ax-mp ) AEFGZBHIZDBJKUATLBCMUABFEBCNBCOUAPQSR $.
        $( [17-Nov-2007] $)
    $}

    ${
      hhims2.2 $e |- D = ( IndMet ` U ) $.
      $( Hilbert space distance metric. $)
      hhims2 $p |- D = ( normh o. -h ) $=
        ( cims cfv cno cmv ccom eqid hhims eqtr4i ) ABEFGHIZDMBCMJKL $.
        $( [10-Apr-2008] $)

      $( The induced metric of Hilbert space. $)
      hhmet $p |- D e. Met $=
        ( cnv wcel cme hhnv imsmet ax-mp ) BEFAGFBCHABDIJ $.
        $( [10-Apr-2008] $)

      $( The base set for the metric for Hilbert space. $)
      hhmetba $p |- ~H = dom dom D $=
        ( hhnv hhba h2hmetba ) ABCBCEBCFDG $.
        $( [10-Apr-2008] $)

      $( Value of the distance function of the metric space of Hilbert
         space. $)
      hhmetdval $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( A D B ) = ( normh ` ( A -h B ) ) ) $=
        ( hhnv hhba h2hmetdval ) ABCDEDEGDEHFI $.
        $( [10-Apr-2008] $)
    $}

    $( The inner product operation of Hilbert space. $)
    hhip $p |- .ih = ( .i ` U ) $=
      ( vx vy csp cfv wceq cv co chil wral wcel cva cno c2 cexp cmv cmin ci cc
      cip wa csm cmul caddc cdiv polid cnv hhnv hhba hhva hhsm hhnm eqid ipval3
      c4 hhvs mp3an1 eqtr4d rgen2a cxp wf wb ax-hfi ipf ax-mp wfn ffn eqfnoprv2
      syl2an mp2an mpbir ) EAUAFZGZCHZDHZEIZVOVPVMIZGZDJKCJKZVSCDJVOJLZVPJLZUBV
      QVOVPMINFOPIVOVPQINFOPIRISVOSVPUCIZMINFOPIVOWCQINFOPIRIUDIUEIUPUFIZVRVOVP
      UGAUHLZWAWBVRWDGABUIZVOVPVMUCAMQNJABUJZABUKABULABUMVMUNZABUQUOURUSUTJJVAZ
      TEVBZWITVMVBZVNVTVCZVDWEWKWFVMAJWGWHVEVFWJEWIVGVMWIVGWLWKWITEVHWITVMVHCDJ
      JEVMVIVJVKVL $.
      $( [16-Nov-2013] $) $( [17-Nov-2007] $)

    $( The Hilbert space of the Hilbert Space Explorer is an inner product
       space. $)
    hhph $p |- U e. CPreHil $=
      ( vx vy wcel cva csm cno co cfv c2 cexp caddc chil cvv cc normcl syl sqcl
      recnd cphl cop cnv cv c1 cneg cmul wceq wral cabl hilabl elisseti hvmulex
      wa wb cr normf ax-hilex fex mp2an w3a eleq1i cgr ablogrpo ax-mp ax-hfvadd
      wf cxp fdmi grprn isphg syl5bb mp3an eqid hhnv cmv normpar fveq2d opreq1d
      hvsubval opreq2d hvaddcl hvsubcl 3syl addcom syl11anc eqtr3d adddi mp3an1
      2cn syl2an 3eqtr4d rgen2a mpbir2an ) AUAEZFGUBHUBZUCEZCUDZDUDZFIZHJZKLIZW
      RUEUFWSGIFIZHJZKLIZMIZKWRHJZKLIZWSHJZKLIZMIUGIZUHZDNUICNUIZFOEZGOEZHOEZWO
      WQXMUNZUOFUJUKULUMNUPHVGNOEXPUQURNUPOHUSUTWOWPUAEXNXOXPVAXQAWPUABVBCDOOOG
      FHNFNFUJEFVCEUKFVDVENNVHNFVFVIVJVKVLVMWPWPVNVOXLCDNWRNEZWSNEZUNZWRWSVPIZH
      JZKLIZXBMIZKXHUGIKXJUGIMIZXFXKWRWSVQXTXBYCMIZXFYDXTYCXEXBMXTYBXDKLXTYAXCH
      WRWSVTVRVSWAXTXBPEZYCPEZYFYDUHXTXAPEYGXTXAXTWTNEXAUPEWRWSWBWTQRTXASRXTYAN
      EZYBPEYHWRWSWCYIYBYAQTYBSWDXBYCWEWFWGXRXHPEZXJPEZXKYEUHZXSXRXGPEYJXRXGWRQ
      TXGSRXSXIPEYKXSXIWSQTXISRKPEYJYKYLWJKXHXJWHWIWKWLWMWN $.
      $( [25-Jul-2008] $) $( [24-Nov-2007] $)
  $}

