$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Relate Hilbert space to normed complex vector spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Hilbert space vector addition is an Abelian group operation. $)
    hilabl $p |- +h e. AbelOp $=
      ( vx vy vz cva chil c0v c1 cneg cv csm co ax-hilex ax-hfvadd ax-hvass
      ax-hv0cl hvaddid2 cc wcel ax1cn negcli hvmulcl mpan wceq ax-hvcom
      mpancom hvnegid eqtrd isgrpoi cxp fdmi isabloi ) ABDEABCFDGHZAIZJKZELMUMB
      IZCINOUMPULQRUMERZUNERZGSTULUMUAUBZUPUNUMDKZUMUNDKZFUQUPUSUTUCURUNUMUDUEU
      MUFUGUHEEUIEDMUJUMUOUDUK $.
      $( [10-Aug-2008] $) $( [15-Apr-2007] $)

    $( The group identity element of Hilbert space vector addition is the zero
       vector. $)
    hilid $p |- ( Id ` +h ) = 0h $=
      ( vx vy c0v cv cva co wceq chil wral cgi cfv hvaddid2 rgen wcel wb
      ax-hv0cl crab cuni opreq1 eqeq1d ralbidv cgr cabl hilabl ablogrpo ax-mp
      cxp ax-hfvadd fdmi grprn eqid grpoidval eqcom eqeq12 sylanb mpan bibi12d
      wreu grpoideu reuuni1 mpan2 vtoclga mpbi ) CADZEFZVDGZAHIZEJKZCGZVFAHVDLM
      CHNVGVIOZPBDZVDEFZVDGZAHIZVNBHQRZVKGZOZVJBCHVKCGZVNVGVPVIVRVMVFAHVRVLVEVD
      VKCVDESTUAVHVOGZVRVPVIOZEUBNZVSEUCNWAUDEUEUFZABVHEHEHWBHHUGHEUHUIUJZVHUKU
      LUFVSVOVHGVRVTVHVOUMVOVHVKCUNUOUPUQVKHNVNBHURZVQWAWDWBABEHWCUSUFVNBHUTVAV
      BUFVC $.
      $( [25-Jul-2008] $) $( [16-Apr-2007] $)

    $( Hilbert space is a complex vector space.  Vector addition is ` +h ` ,
       and scalar product is ` .h ` . $)
    hilvc $p |- <. +h , .h >. e. CVec $=
      ( vx vy vz csm cva cop chil hilabl cxp ax-hfvadd fdmi ax-hfvmul cv
      ax-hvmulid ax-hvdistr1 ax-hvdistr2 ax-hvmulass eqid isvci ) ABCDEEDFZGHGG
      IGEJKLAMZNBMZUACMZOUBUCUAPUBUCUAQTRS $.
      $( [15-Apr-2007] $)
  $}

  ${
    $d x y U $.
    hilnorm.5 $e |- ~H = ( BaseSet ` U ) $.
    hilnorm.2 $e |- .ih = ( .i ` U ) $.
    hilnorm.9 $e |- U e. NrmCVec $.
    $( Hilbert space norm in terms of vector space norm. $)
    hilnormi $p |- normh = ( norm ` U ) $=
      ( vx vy cv chil wcel cnm cfv wceq wa copab csp co csqr cno cnv eqid ipnm
      mpan eqeq2d pm5.32i opabbii wfn cr wf nvf ax-mp ffn dffn5 mpbi dfhnorm2
      3eqtr4ri ) EGZHIZFGZUPAJKZKZLZMZEFNZUQURUPUPOPQKZLZMZEFNUSRVBVFEFUQVAVEUQ
      UTVDURASIZUQUTVDLDUPOAUSHBUSTZCUAUBUCUDUEUSHUFZUSVCLHUGUSUHZVIVGVJDAUSHBV
      HUIUJHUGUSUKUJEFHUSULUMEFUNUO $.
      $( [11-Sep-2007] $)
  $}

  ${
    hilhh.1 $e |- ~H = ( BaseSet ` U ) $.
    hilhh.2 $e |- +h = ( +v ` U ) $.
    hilhh.3 $e |- .h = ( .s ` U ) $.
    hilhh.5 $e |- .ih = ( .i ` U ) $.
    hilhh.9 $e |- U e. NrmCVec $.
    $( Deduce the structure of Hilbert space from its components. $)
    hilhhi $p |- U = <. <. +h , .h >. , normh >. $=
      ( cnv wcel cva csm cop cno wceq hilnormi nvop ax-mp ) AGHAIJKLKMFJAILCDAB
      EFNOP $.
      $( [10-Apr-2008] $)
  $}

  ${
    $d k v w x y z U $.
    hhnv.1 $e |- U = <. <. +h , .h >. , normh >. $.
    $( Hilbert space is a normed complex vector space. $)
    hhnv $p |- U e. NrmCVec $=
      ( vx vy csm cva cno chil c0v cabl wcel cgr hilabl ablogrpo ax-mp cxp
      ax-hfvadd fdmi grprn cgi cfv hilid eqcomi hilvc normf cv cc0 wceq norm-i
      biimpa norm-iii norm-ii isnvi ) CDEAFGHIFHFJKFLKMFNOHHPHFQRSFTUAIUBUCUDUE
      CUFZHKUNGUAUGUHUNIUHUNUIUJDUFZUNUKUNUOULBUM $.
      $( [25-Jul-2008] $) $( [17-Nov-2007] $)

    $( The group (addition) operation of Hilbert space. $)
    hhva $p |- +h = ( +v ` U ) $=
      ( hhnv h2hva ) ABABCD $.
      $( [17-Nov-2007] $)

    $( The base set of Hilbert space.  This theorem provides an independent
       proof of ~ df-hba (see comments in that definition). $)
    hhba $p |- ~H = ( BaseSet ` U ) $=
      ( chil cva crn cba cfv cabl wcel cgr hilabl ablogrpo ax-mp cxp ax-hfvadd
      fdmi grprn eqid hhva bafval eqtr4i ) CDEAFGZDCDHIDJIKDLMCCNCDOPQADUBUBRAB
      STUA $.
      $( [25-Jul-2008] $) $( [17-Nov-2007] $)

    $( The zero vector of Hilbert space. $)
    hh0v $p |- 0h = ( 0vec ` U ) $=
      ( cn0v cfv cpv cgi cva c0v eqid 0vfval hhva fveq2i hilid 3eqtr2ri ) ACDZA
      EDZFDGFDHAPOPIOIJGPFABKLMN $.
      $( [17-Nov-2007] $)

    $( The scalar product operation of Hilbert space. $)
    hhsm $p |- .h = ( .s ` U ) $=
      ( hhnv h2hsm ) ABABCD $.
      $( [17-Nov-2007] $)

    $( The vector subtraction operation of Hilbert space. $)
    hhvs $p |- -h = ( -v ` U ) $=
      ( hhnv hhba h2hvs ) ABABCABDE $.
      $( [13-Dec-2007] $)

    $( The norm function of Hilbert space. $)
    hhnm $p |- normh = ( norm ` U ) $=
      ( hhnv h2hnm ) ABABCD $.
      $( [17-Nov-2007] $)

    ${
      hhims.2 $e |- D = ( normh o. -h ) $.
      $( The induced metric of Hilbert space. $)
      hhims $p |- D = ( IndMet ` U ) $=
        ( cno cmv ccom cims cfv cnv wcel wceq hhnv hhvs hhnm eqid imsval ax-mp
        eqtr4i ) AEFGZBHIZDBJKUATLBCMUABFEBCNBCOUAPQRS $.
        $( [17-Nov-2007] $)
    $}

    ${
      hhims2.2 $e |- D = ( IndMet ` U ) $.
      $( Hilbert space distance metric. $)
      hhims2 $p |- D = ( normh o. -h ) $=
        ( cims cfv cno cmv ccom eqid hhims eqtr4i ) ABEFGHIZDMBCMJKL $.
        $( [10-Apr-2008] $)

      $( The induced metric of Hilbert space. $)
      hhmet $p |- D e. Met $=
        ( cnv wcel cme hhnv imsmet ax-mp ) BEFAGFBCHABDIJ $.
        $( [10-Apr-2008] $)

      $( The base set for the metric for Hilbert space. $)
      hhmetba $p |- ~H = dom dom D $=
        ( hhnv hhba h2hmetba ) ABCBCEBCFDG $.
        $( [10-Apr-2008] $)

      $( Value of the distance function of the metric space of Hilbert
         space. $)
      hhmetdval $p |- ( ( A e. ~H /\ B e. ~H ) ->
                  ( A D B ) = ( normh ` ( A -h B ) ) ) $=
        ( hhnv hhba h2hmetdval ) ABCDEDEGDEHFI $.
        $( [10-Apr-2008] $)
    $}

    $( The inner product operation of Hilbert space. $)
    hhip $p |- .ih = ( .i ` U ) $=
      ( vx vy vz vw vv vk csp cip cfv wceq chil cxp cv co wral wfn wa wb cc wf
      ax-hfi ffn ax-mp c1 c4 cfz ci cexp csm cva cno c2 cmul csu cdiv oprex
      cnv wcel copab2 hhnv hhba hhva hhsm hhnm eqid ipfval fnoprab2 eqfnoprv
      mp2an cmv cmin caddc polid hhvs ipval3 mp3an1 eqtr4d rgen2a mpbir2an ) IA
      JKZLZMMNZWDLZCOZDOZIPZWFWGWBPZLZDMQCMQZIWDRZWBWDRWCWEWKSTWDUAIUBWLUCWDUAI
      UDUEEFGMMUFUGUHPUIHOUJPZEOZWMFOZUKPULPUMKUNUJPUOPHUPZUGUQPZWBWPUGUQURAUSU
      TZWBWNMUTWOMUTSGOWQLSEFGVALABVBZEFGWBUKAHULUMMABVCZABVDZABVEZABVFZWBVGZVH
      UEVICDMMMMIWBVJVKWDVGWJCDMWFMUTZWGMUTZSWHWFWGULPUMKUNUJPWFWGVLPUMKUNUJPVM
      PUIWFUIWGUKPZULPUMKUNUJPWFXGVLPUMKUNUJPVMPUOPVNPUGUQPZWIWFWGVOWRXEXFWIXHL
      WSWFWGWBUKAULVLUMMWTXAXBXCXDABVPVQVRVSVTWA $.
      $( [17-Nov-2007] $)

    $( The Hilbert space of the Hilbert Space Explorer is an inner product
       space. $)
    hhph $p |- U e. CPreHil $=
      ( vx vy cphl wcel cva csm cop cno cnv cv co cfv c2 cexp c1 cneg caddc
      cmul wceq chil wral cvv wa wb cabl hilabl elisseti hvmulex cr wf normf
      ax-hilex fex mp2an w3a cgr ablogrpo ax-mp cxp ax-hfvadd fdmi grprn isphg
      eleq1i syl5bb mp3an eqid hhnv cmv normpar hvsubval fveq2d opreq1d
      opreq2d cc hvaddcl normcl syl recnd sqcl hvsubcl 3syl addcom syl11anc
      eqtr3d 2cn adddi mp3an1 syl2an 3eqtr4d rgen2a mpbir2an ) AEFZGHIJIZKFZCLZ
      DLZGMZJNZOPMZWRQRWSHMGMZJNZOPMZSMZOWRJNZOPMZWSJNZOPMZSMTMZUAZDUBUCCUBUCZG
      UDFZHUDFZJUDFZWOWQXMUEZUFGUGUHUIUJUBUKJULUBUDFXPUMUNUBUKUDJUOUPXNXOXPUQWP
      EFXQWOCDUDUDUDHGJUBGUBGUGFGURFUHGUSUTUBUBVAUBGVBVCVDVEAWPEBVFVGVHWPWPVIVJ
      XLCDUBWRUBFZWSUBFZUEZWRWSVKMZJNZOPMZXBSMZOXHTMOXJTMSMZXFXKWRWSVLXTXBYCSMZ
      XFYDXTYCXEXBSXTYBXDOPXTYAXCJWRWSVMVNVOVPXTXBVQFZYCVQFZYFYDUAXTXAVQFYGXTXA
      XTWTUBFXAUKFWRWSVRWTVSVTWAXAWBVTXTYAUBFZYBVQFYHWRWSWCYIYBYAVSWAYBWBWDXBYC
      WEWFWGXRXHVQFZXJVQFZXKYEUAZXSXRXGVQFYJXRXGWRVSWAXGWBVTXSXIVQFYKXSXIWSVSWA
      XIWBVTOVQFYJYKYLWHOXHXJWIWJWKWLWMWN $.
      $( [25-Jul-2008] $) $( [24-Nov-2007] $)
  $}

