$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Atoms, exchange and covering properties, atomicity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.
    $( The Hilbert lattice has the covering property.  Proposition 1(ii) of
       [Kalmbach] p. 140 (and its converse). $)
    chcv1t $p |- ( ( A e. CH /\ B e. Atoms ) ->
                ( -. B (_ A <-> A <o ( A vH B ) ) ) $=
      ( vx cch wcel cat wa wss wn chj co ccv wbr cv c0v wne csn cspn cfv wceq
      chil wrex wi sseq1 negbid opreq2 breq2d imbi12d spansncv2t syl5cbir
      adantld r19.23adva imp atom1d sylan2b wpss chjclt cvpsst syldan chnlet
      sylibrd atelch sylan2 impbid ) ADEZBFEZGBAHZIZAABJKZLMZVECNZOPZBVKQRSZTZG
      ZCUAUBZVHVJUCZVFVEVPVQVEVOVQCUAVEVKUAEGZVNVQVLVNVQVMAHZIZAAVMJKZLMZUCVRVN
      VHVTVJWBVNVGVSBVMAUDUEVNVIWAALBVMAJUFUGUHAVKUIUJUKULUMCBUNUOVEBDEZVJVHUCV
      FVEWCGVJAVIUPZVHVEWCVIDEVJWDUCABUQAVIURUSABUTVABVBVCVD $.
      $( [26-Jul-2004] $) $( [11-Jun-2004] $)

    $( The Hilbert lattice has the covering property. $)
    chcv2t $p |- ( ( A e. CH /\ B e. Atoms ) ->
                ( A (. ( A vH B ) <-> A <o ( A vH B ) ) ) $=
      ( cch wcel cat wa wss wn chj co wpss ccv wbr wb chnlet atelch sylan2
      chcv1t bitr3d ) ACDZBEDZFBAGHZAABIJZKZAUCLMTBCDUBUDNUAABOBPQABRS $.
      $( [26-Jul-2004] $) $( [11-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The join of a closed subspace and an atom equals their subspace sum.
       Special case of remark in [Kalmbach] p. 65, stating that if ` A ` or
       ` B ` is finite dimensional, then this equality holds. $)
    chjatom $p |- ( ( A e. CH /\ B e. Atoms ) -> ( A +H B ) = ( A vH B ) ) $=
      ( vx cch wcel cat cph co chj wceq cv c0v wne csn cspn cfv wa chil wrex
      wi opreq2 eqeq12d spansnjt syl5bir exp3a adantl com3l r19.23adv atom1d
      syl5ib imp ) ADEZBFEZABGHZABIHZJZULCKZLMZBUQNOPZJZQZCRSUPUMULVAUPCRVAULUQ
      REZUPUTULVBUPTTURUTULVBUPUTUPAUSGHZAUSIHZJULVBQUTUNVCUOVDBUSAGUABUSAIUAUB
      AUQUCUDUEUFUGUHCBUIUJUK $.
      $( [27-Feb-2005] $) $( [4-Jun-2004] $)
  $}

  ${
    $d x y A $.
    shatomic.1 $e |- A e. SH $.
    $( The lattice of Hilbert subspaces is atomic, i.e. any non-zero element is
       greater than or equal to some atom.  Part of proof of Theorem 16.9 of
       [MaedaMaeda] p. 70. $)
    shatomic $p |- ( A =/= 0H -> E. x e. Atoms x (_ A ) $=
      ( vy c0h wne cv c0v wrex wss cat shne0 csn cort cfv wcel wa sseq1
      rcla4ev chil h1dat shel sylan csh sh1dle mpan adantr sylanc r19.23aiva
      sylbi ) BEFDGZHFZDBIAGZBJZAKIZDBCLULUODBUKMNONOZKPZUPBJZUOUKBPZULQUNURAUP
      KUMUPBRSUKTPULUQUSUKUAUKBCUBUCUSURULBUDPUSURCBUKUEUFUGUHUIUJ $.
      $( [30-Jun-2006] $) $( [24-Nov-2004] $)
  $}

  ${
    $d x y A $.
    hatomic.1 $e |- A e. CH $.
    $( The Hilbert lattice is atomic, i.e. any non-zero element is greater than
       or equal to some atom.  Remark in [Kalmbach] p. 140. $)
    hatomic $p |- ( A =/= 0H -> E. x e. Atoms x (_ A ) $=
      ( chshi shatomic ) ABBCDE $.
      $( [2-Jul-2006] $) $( [22-Jul-2001] $)
  $}

  ${
    $d x y A $.
    $( A Hilbert lattice is atomic, i.e. any non-zero element is greater than
       or equal to some atom.  Remark in [Kalmbach] p. 140.  Also Definition
       3.4-2 in [MegillPavicic] p. 2345 (PDF p. 8). $)
    hatomict $p |- ( ( A e. CH /\ A =/= 0H ) -> E. x e. Atoms x (_ A ) $=
      ( cch wcel c0h wne cv wss cat wrex wi cif wceq neeq1 sseq2 rexbidv
      imbi12d h0elch elimel hatomic dedth imp ) BCDZBEFZAGZBHZAIJZUCUDUGKUCBELZ
      EFZUEUHHZAIJZKBEBUHMZUDUIUGUKBUHENULUFUJAIBUHUEOPQAUHBECRSTUAUB $.
      $( [7-Oct-2006] $) $( [24-Jun-2004] $)
  $}

  ${
    $d x y A $.
    shatomistic.1 $e |- A e. SH $.
    $( The lattice of Hilbert subspaces is atomistic, i.e. any element is the
       supremum of its atoms.  Part of proof of Theorem 16.9 of [MaedaMaeda]
       p. 70. $)
    shatomistic $p |- A = ( span ` U. { x e. Atoms | x (_ A } ) $=
      ( vy cv wss cat crab cuni cspn cfv wcel c0v eleq1 wne wa csn wceq chil
      csh shel spansnsht spanid 3syl adantr spansnat sylan spansnsst mpan jca
      sseq1 elrab sylibr elssuni cch atssch chsssh sstri rabss2 ax-mp uniss
      unimax shssi eqsstr spanss eqsstr3d spansnid syl sseldd spanclt sh0 a1i
      pm2.61ne ssriv mp2an fveq2i eqtr sseqtr eqssi ) BAEZBFZAGHZIZJKZDBWDDEZBL
      ZWEWDLMWDLZWEMWEMWDNWFWEMOZPZWEQJKZWDWEWIWJWJJKZWDWFWKWJRZWHWFWESLZWJTLWL
      WEBCUAZWEUBWJUCUDUEWIWJWBLZWJWCFZWKWDFZWIWJGLZWJBFZPWOWIWRWSWMWHWRWFWEUFW
      NUGWFWSWHBTLZWFWSCBWEUHUIUEUJWAWSAWJGVTWJBUKULUMWJWBUNWCSFZWPWQWCWAATHZIZ
      SWBXBFZWCXCFZGTFXDGUOTUPUQURWAAGTUSUTWBXBVAUTZXCBSWTXCBRCABTVBUTZBCVCVDZU
      RZWJWCVEUIUDVFWFWEWJLZWHWFWMXJWNWEVGVHUEVIWGWFWDTLZWGXAXKXIWCVJUTWDVKUTVL
      VMVNWDXCJKZBXCSFXEWDXLFXHXFWCXCVEVOXLBJKZBXCBJXGVPWTXMBRCBUCUTVQVRVS $.
      $( [16-Jan-2005] $) $( [26-Nov-2004] $)
  $}

  ${
    $d x y A $.
    hatomistic.1 $e |- A e. CH $.
    $( ` CH ` is atomistic, i.e. any element is the supremum of its atoms.
       Remark in [Kalmbach] p. 140. $)
    hatomistic $p |- A = ( \/H ` { x e. Atoms | x (_ A } ) $=
      ( vy cv wss cat crab chsup cfv cch wcel ssrab2 atssch sstri chsupclt
      ax-mp chshi wa atelch anim1i sseq1 elrab 3imtr4 ssriv wi chsupss mp2an
      wceq chsupid sseqtr cort cin wrex wn c0h cuni elssuni sylbir chsupunss
      sstr2 mpi syl ex wne atn0 adantr wb chle0t ssin chocin sseq2i bitr2
      syl5bbr biimpa exp32 imp necon3ad mpd syld imnan sylib negbii nrex
      choccl chincl hatomic necon1bi omlsi eqcomi ) AEZBFZAGHZIJZBWNBWMKFZWNKLW
      MGKWLAGMNOZWMPQZBCRWNWLAKHZIJZBWMWRFZWNWSFZDWMWRDEZGLZXBBFZSZXBKLZXDSXBWM
      LZXBWRLXCXFXDXBTZUAWLXDAXBGWKXBBUBZUCZWLXDAXBKXIUCUDUEWOWRKFWTXAUFWPWLAKM
      WMWRUGUHQBKLWSBUICABUJQUKXBBWNULJZUMZFZDGUNZUOXLUPUIXMDGXCXDXBXKFZSZUOZXM
      UOXCXDXOUOZUFXQXCXDXBWNFZXRXCXDXSXEXBWMUQZFZXSXEXGYAXJXBWMURUSYAXTWNFZXSW
      OYBWPWMUTQXBXTWNVAVBVCVDXCXSXRXCXSSZXBUPVEZXRXCYDXSXBVFVGYCXOXBUPXCXSXOXB
      UPUIZUFXCXSXOYEXCXSXOSZYEXCXBUPFZYEYFXCXFYGYEVHXHXBVIVCYFXBWNXKUMZFYGXBWN
      XKVJYHUPXBWNWQVKVLVMVNVOVPVQVRVSVDVTXDXOWAWBXPXMXBBXKVJWCWBWDXNXLUPDXLBXK
      CWNWQWEWFWGWHQWIWJ $.
      $( [14-Aug-2002] $)
  $}

  ${
    $d x A $.  $d x B $.
    chpssat.1 $e |- A e. CH $.
    chpssat.2 $e |- B e. CH $.
    $( Two Hilbert lattice elements in a proper subset relationship imply
       the existence of an atom less than or equal to one but not the other. $)
    chpssat $p |- ( A (. B -> E. x e. Atoms ( x (_ B /\ -. x (_ A ) ) $=
      ( wpss wss wn cv wa cat wrex dfpss3 pm3.27bi wral wi iman ralbii crab
      ss2rab chsup cfv cch ssrab2 atssch sstri chsupss mp2an hatomistic
      3sstr4g sylbir con3i dfrex2 sylibr syl ) BCFZCBGZHZAIZCGZUSBGZHJZAKLZUPBC
      GURBCMNURVBHZAKOZHVCVEUQVEUTVAPZAKOZUQVFVDAKUTVAQRVGUTAKSZVAAKSZGZUQUTVAA
      KTVJVHUAUBZVIUAUBZCBVHUCGVIUCGVJVKVLGPVHKUCUTAKUDUEUFVIKUCVAAKUDUEUFVHVIU
      GUHACEUIABDUIUJUKUKULVBAKUMUNUO $.
      $( [26-Jul-2004] $) $( [10-Jun-2004] $)

    $( The Hilbert lattice is relatively atomic.  Remark 2 of [Kalmbach]
       p. 149. $)
    chrelat $p |- ( A (. B ->
                  E. x e. Atoms ( A (. ( A vH x ) /\ ( A vH x ) (_ B ) ) $=
      ( wpss cv wss wn wa cat wrex chj co chpssat wcel cch wb chnlet mpan
      adantl ibar chlubt mp3an13 sylan9bb anbi12d pssss atelch syl2an ancom
      syl5bb rexbidva mpbid ) BCFZAGZCHZUOBHIZJZAKLBBUOMNZFZUSCHZJZAKLABCDEOUNU
      RVBAKUNUOKPZJUQUPJZVBURBCHZUOQPZVDVBRUNVCVEVFJUQUTUPVAVFUQUTRZVEBQPZVFVGD
      BUOSTUAVEUPVEUPJZVFVAVEUPUBVHVFCQPVIVARDEBUOCUCUDUEUFBCUGUOUHUIUPUQUJUKUL
      UM $.
      $( [26-Jul-2004] $) $( [11-Jun-2004] $)

    $( A consequence of relative atomicity. $)
    chrelat2 $p |- ( -. A (_ B <-> E. x e. Atoms ( x (_ A /\ -. x (_ B ) ) $=
      ( wss wn cv wa cat wrex cin wpss nssinpss chj co chincl chrelat wcel cch
      wi atelch wb chlubt mp3an13 pm3.27 syl6bir adantld chnlet mpan ssin
      negbii syl5bb anbi12d pm3.21 wo ianor pm4.13 orbi1i orcom imor bitr4
      3bitr2r sylib con2i adantrl jcad syl r19.22i sylbi wral sstr2 com12 a1d
      r19.21aiv iman ralbii ralnex bitr impbi ) BCFZGZAHZBFZWCCFZGZIZAJKZWBBCLZ
      BMZWHBCNWJWIWIWCOPZMZWKBFZIZAJKWHAWIBBCDEQZDRWNWGAJWCJSZWCTSZWNWGUAWCUBWQ
      WNWDWFWQWMWDWLWQWMWIBFZWDIZWDWITSZWQBTSWSWMUCWODWIWCBUDUEZWRWDUFUGUHWQWNW
      DWEIZGZWSIWFWQXCWLWSWMWQWCWIFZGZWLXCWTWQXEWLUCWOWIWCUIUJXBXDWCBCUKULUMXAU
      NXCWDWFWRWEXCWDIZWEWDXBUAZXFGZWEWDUOXHXCGZWDGZUPXBXJUPZXGXCWDUQXBXIXJXBUR
      USXKXJXBUPXGXBXJUTWDXBVAVBVCVDVEVFUGVGVHVIVHVJWAWHWAWDWEUAZAJVKZWHGZWAXLA
      JWAXLWPWDWAWEWCBCVLVMVNVOXMWGGZAJVKXNXLXOAJWDWEVPVQWGAJVRVSVDVEVT $.
      $( [26-Jul-2004] $) $( [30-Jun-2004] $)

    $( If a Hilbert lattice element covers another, it equals the other joined
       with some atom.  This is a consequence of the relative atomicity of
       Hilbert space. $)
    cvat $p |- ( A <o B -> E. x e. Atoms ( A vH x ) = B ) $=
      ( ccv wbr cv chj co wpss wss wa cat wrex wceq cch wcel wi cvpsst mp2an
      chrelat syl atelch chjclt mpan cvnbtwn2t mp3an12 com12 r19.22dv mpd ) BCF
      GZBBAHZIJZKUNCLMZANOZUNCPZANOULBCKZUPBQRZCQRZULURSDEBCTUAABCDEUBUCULUOUQA
      NUMNRZULUOUQSZVAUNQRZULVBSZVAUMQRZVCUMUDUSVEVCDBUMUEUFUCUSUTVCVDDEBCUNUGU
      HUCUIUJUK $.
      $( [30-Mar-2005] $) $( [30-Nov-2004] $)

    $( An alternate way to express the covering property. $)
    cvbr3 $p |- ( A <o B <-> ( A (. B /\ E. x e. Atoms ( A vH x ) = B ) ) $=
      ( ccv wbr wpss cv chj co wceq cat wrex wa cch wcel wi cvpsst mp2an cvat
      jca wb chcv2t mpan adantr psseq2 adantl breq2 3bitr3d biimpd ex com3r
      r19.23adv imp impbi ) BCFGZBCHZBAIZJKZCLZAMNZOUQURVBBPQZCPQUQURRDEBCSTABC
      DEUAUBURVBUQURVAUQAMUSMQZVAURUQVDVAURUQRVDVAOZURUQVEBUTHZBUTFGZURUQVDVFVG
      UCZVAVCVDVHDBUSUDUEUFVAVFURUCVDUTCBUGUHVAVGUQUCVDUTCBFUIUHUJUKULUMUNUOUP
      $.
      $( [10-Jun-2005] $) $( [30-Nov-2004] $)

    $( Lemma for ~ cvexch . $)
    cvexchlem $p |- ( ( A i^i B ) <o B -> A <o ( A vH B ) ) $=
      ( vx cv wss cin wn wa cat wrex ccv wbr chj co wpss cch wcel wi chincl
      cvpsst mp2an chpssat syl wb chcv1t mpan biimpa ssin ancom bitr3 baibr
      negbid biimpar sylan2 adantrr wceq chjasst mp3an12 chabs1 opreq1i
      syl5reqr adantr chnlet chlubt mp3an13 inss2 biantrur syl5bb anbi12d
      chjclt cvnbtwn2t com23 sylbid imp32 opreq2d eqtr3d atelch sylan breqtrd
      exp32 r19.23aiv mpcom ) EFZBGZWEABHZGZIZJZEKLZWGBMNZAABOPZMNZWLWGBQZWKWGR
      SZBRSZWLWOTABCDUAZDWGBUBUCEWGBWRDUDUEWJWLWNTEKWEKSZWJWLWNWSWJWLJZJAAWEOPZ
      WMMWSWJAXAMNZWLWSWEAGZIZXBWJWSXDXBARSZWSXDXBUFCAWEUGUHUIWFXDWIWFXCWHWHWFX
      CWHXCWFJWFXCJWEABUJXCWFUKULUMUNUOUPUQWERSZWTXAWMURWSXFWTJZAWGWEOPZOPZXAWM
      XFXIXAURWTXFAWGOPZWEOPZXIXAXEWPXFXKXIURCWRAWGWEUSUTXJAWEOABCDVAVBVCVDXGXH
      BAOXFWJWLXHBURZXFWJWGXHQZXHBGZJZWLXLTXFWIWFJXOWJXFWIXMWFXNWPXFWIXMUFWRWGW
      EVEUHXFWGBGZWFJZXNWFWPXFWQXQXNUFWRDWGWEBVFVGXPWFABVHVIVJVKWFWIUKVJXFWLXOX
      LXFXHRSZWLXOXLTTZWPXFXRWRWGWEVLUHWPWQXRXSWRDWGBXHVMUTUEVNVOVPVQVRWEVSVTWA
      WBWCWD $.
      $( [26-Jul-2004] $) $( [10-Jun-2004] $)

    $( The Hilbert lattice satisfies the exchange axiom.  Proposition 1(iii) of
       [Kalmbach] p. 140 and its converse.  Originally proved by Garrett
       Birkhoff in 1933. $)
    cvexch $p |- ( ( A i^i B ) <o B <-> A <o ( A vH B ) ) $=
      ( cin ccv wbr chj co cvexchlem cort cfv choccl chdmj1 incom eqtr breq1i
      chdmm1 chjcom breq2i 3imtr4 cch wcel wb chjcl cvcon3t mp2an chincl impbi
      ) ABEZBFGZAABHIZFGZABCDJULKLZAKLZFGZBKLZUJKLZFGZUMUKUQUOEZUOFGUQUQUOHIZFG
      UPUSUQUOBDMZACMZJUNUTUOFUNUOUQEUTABCDNUOUQOPQURVAUQFURUOUQHIVAABCDRUOUQVC
      VBSPTUAAUBUCULUBUCUMUPUDCABCDUEAULUFUGUJUBUCBUBUCUKUSUDABCDUHDUJBUFUGUAUI
      $.
      $( [26-Jul-2004] $) $( [12-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A consequence of relative atomicity. $)
    chrelat2t $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( -. A (_ B <-> E. x e. Atoms ( x (_ A /\ -. x (_ B ) ) ) $=
      ( cch wcel wss wn cv wa cat wrex wb chil cif wceq sseq1 negbid sseq2
      anbi1d rexbidv bibi12d anbi2d helch elimel chrelat2 dedth2h ) BDEZCDEZBCF
      ZGZAHZBFZUKCFZGZIZAJKZLUGBMNZCFZGZUKUQFZUNIZAJKZLUQUHCMNZFZGZUTUKVCFZGZIZ
      AJKZLBCMMBUQOZUJUSUPVBVJUIURBUQCPQVJUOVAAJVJULUTUNBUQUKRSTUACVCOZUSVEVBVI
      VKURVDCVCUQRQVKVAVHAJVKUNVGUTVKUMVFCVCUKRQUBTUAAUQVCBMDUCUDCMDUCUDUEUF $.
      $( [27-Jul-2004] $) $( [1-Jul-2004] $)

    $( A consequence of relative atomicity. $)
    chrelat3t $p |- ( ( A e. CH /\ B e. CH ) ->
                   ( A (_ B <-> A. x e. Atoms ( x (_ A -> x (_ B ) ) ) $=
      ( cch wcel wa wss cv wn cat wral wi wrex chrelat2t dfrex2 syl6bb con4bid
      iman ralbii syl6bbr ) BDECDEFZBCGZAHZBGZUCCGZIFZIZAJKZUDUELZAJKUAUBUHUAUB
      IUFAJMUHIABCNUFAJOPQUIUGAJUDUERST $.
      $( [27-Jul-2004] $) $( [2-Jul-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    chrelat3.1 $e |- A e. CH $.
    chrelat3.2 $e |- B e. CH $.
    $( A consequence of the relative atomicity of Hilbert space: the ordering
       of Hilbert lattice elements is completely determined by the atoms they
       majorize. $)
    chrelat3 $p |- ( A (_ B <-> A. x e. Atoms ( x (_ A -> x (_ B ) ) $=
      ( cch wcel wss cv wi cat wral wb chrelat3t mp2an ) BFGCFGBCHAIZBHPCHJAKLM
      DEABCNO $.
      $( [29-May-2005] $) $( [30-Jun-2004] $)

    $( A consequence of relative atomicity.  Extensionality principle:
       two lattice elements are equal iff they majorize the same atoms. $)
    chrelat4 $p |- ( A = B <-> A. x e. Atoms ( x (_ A <-> x (_ B ) ) $=
      ( wss wa cv wi cat wral wceq wb chrelat3 anbi12i eqss bi ralbii r19.26
      bitr 3bitr4 ) BCFZCBFZGAHZBFZUDCFZIZAJKZUFUEIZAJKZGZBCLUEUFMZAJKZUBUHUCUJ
      ABCDENACBEDNOBCPUMUGUIGZAJKUKULUNAJUEUFQRUGUIAJSTUA $.
      $( [26-Jul-2005] $) $( [30-Jun-2004] $)
  $}

  $( The Hilbert lattice satisfies the exchange axiom.  Proposition 1(iii) of
     [Kalmbach] p. 140 and its converse.  Originally proved by Garrett
     Birkhoff in 1933. $)
  cvexcht $p |- ( ( A e. CH /\ B e. CH ) ->
                ( ( A i^i B ) <o B <-> A <o ( A vH B ) ) ) $=
    ( cch wcel cin ccv wbr chj co wb chil cif wceq ineq1 breq1d id opreq1
    breq12d bibi12d ineq2 opreq2 breq2d helch elimel cvexch dedth2h ) ACDZBCDZA
    BEZBFGZAABHIZFGZJUGAKLZBEZBFGZUMUMBHIZFGZJUMUHBKLZEZURFGZUMUMURHIZFGZJABKKA
    UMMZUJUOULUQVCUIUNBFAUMBNOVCAUMUKUPFVCPAUMBHQRSBURMZUOUTUQVBVDUNUSBURFBURUM
    TVDPRVDUPVAUMFBURUMHUAUBSUMURAKCUCUDBKCUCUDUEUF $.
    $( [27-Jul-2004] $) $( [21-Jun-2004] $)

  $( The Hilbert lattice satisfies the covering property of Definition 7.4
     of [MaedaMaeda] p. 31 and its converse. $)
  cvp $p |- ( ( A e. CH /\ B e. Atoms ) ->
            ( ( A i^i B ) = 0H <-> A <o ( A vH B ) ) ) $=
    ( cch wcel cat wa cin ccv wbr c0h wceq chj co wb atcveq0 chinclt atelch
    sylan2 pm3.27 sylanc cvexcht bitr3d ) ACDZBEDZFZABGZBHIZUFJKZAABLMHIZUFCDZU
    DUGUHNUEUFBOUCBCDZUJUDABPBQZRUCUDSTUCUKUGUINUDABUAULRUB $.
    $( [27-Jul-2004] $) $( [21-Jun-2004] $)

  $( The meet of a Hilbert lattice element and an incomparable atom is the zero
     subspace. $)
  atnssm0 $p |- ( ( A e. CH /\ B e. Atoms ) ->
             ( -. B (_ A <-> ( A i^i B ) = 0H ) ) $=
    ( cch wcel cat wa wss wn chj co ccv wbr cin c0h wceq chcv1t cvp bitr4d ) AC
    DBEDFBAGHAABIJKLABMNOABPABQR $.
    $( [27-Jul-2004] $) $( [30-Jun-2004] $)

  $( The meet of distinct atoms is the zero subspace. $)
  atnem0 $p |- ( ( A e. Atoms /\ B e. Atoms ) ->
             ( -. A = B <-> ( A i^i B ) = 0H ) ) $=
    ( cat wcel wa wss wn wceq cin c0h wb atsseq eqcom syl6bb ancoms negbid cch
    atnssm0 atelch sylan bitr3d ) ACDZBCDZEZBAFZGZABHZGABIJHZUDUEUGUCUBUEUGKUCU
    BEUEBAHUGBALBAMNOPAQDUCUFUHKUBABRASTUA $.
    $( [27-Jul-2004] $) $( [25-Jun-2004] $)

  $( The meet with an atom's superset is the atom. $)
  atssmat $p |- ( ( A e. Atoms /\ B e. CH ) ->
            ( A (_ B <-> ( A i^i B ) e. Atoms ) ) $=
    ( cat wcel cch wa wss cin wi wceq df-ss biimp eleq1d biimprcd adantr c0h
    wn wb atnssm0 ancoms biimpd con1d incom eleq1i wne atn0 df-ne sylib sylbi
    syl5 impbid ) ACDZBEDZFZABGZABHZCDZULUOUQIUMUOUQULUOUPACUOUPAJABKLMNOUNBAHZ
    PJZQZUOUQUNUOUSUNUOQZUSUMULVAUSRBASTUAUBUQURCDZUTUPURCABUCUDVBURPUEUTURUFUR
    PUGUHUIUJUK $.
    $( [2-Jul-2006] $) $( [12-Jun-2006] $)

  $( Two atoms covering the zero subspace are equal. $)
  atcv0eq $p |- ( ( A e. Atoms /\ B e. Atoms ) ->
               ( 0H <o ( A vH B ) <-> A = B ) ) $=
    ( cat wcel wa c0h chj co ccv wbr wceq wn cin atnem0 cch wb cvp atelch
    sylan atcv0 adantr biantrurd 3bitrd wi chjclt h0elch cvntrt mp3an1 syldan
    syl2an sylbid a3d opreq1 chjidmt syl sylan9eq eqcomd eleq1d ex ibd syl6com
    adantl impbid ) ACDZBCDZEZFABGHZIJZABKZVFVIVHVFVILZFAIJZAVGIJZEZVHLZVFVJABM
    FKZVLVMABNAODZVEVOVLPVDABQARZSVFVKVLVDVKVEATUAUBUCVPBODZVMVNUDZVDVEVPVRVGOD
    ZVSABUEFODVPVTVSUFFAVGUGUHUIVQBRZUJUKULVEVIVHUDVDVIVEVGCDZVHVIVEWBVIVEVEWBP
    VIVEEZBVGCWCVGBVIVEVGBBGHZBABBGUMVEVRWDBKWABUNUOUPUQURUSUTVGTVAVBVC $.
    $( [29-Jul-2004] $) $( [26-Jun-2004] $)

  $( Two atoms covering the zero subspace are equal. $)
  atcv1t $p |- ( ( ( A e. CH /\ B e. Atoms /\ C e. Atoms ) /\
                A <o ( B vH C ) ) -> ( A = 0H <-> B = C ) ) $=
    ( cch wcel cat w3a chj co ccv wbr wa c0h wceq wi breq1 atcv0eq sylan9bbr
    biimpd ex com23 3adant1 imp wb atcveq0 opreq1 atelch chjidmt syl sylan9eq
    eqcomd eleq1d ibd impcom sylan2 exp32 com34 3adant2 impbid ) ADEZBFEZCFEZGZ
    ABCHIZJKZLAMNZBCNZVCVEVFVGOZVAVBVEVHOUTVAVBLZVFVEVGVIVFVEVGOVIVFLVEVGVFVEMV
    DJKVIVGAMVDJPBCQRSTUAUBUCVCVEVGVFOZUTVBVEVJOZVAUTVBVKUTVBVGVEVFUTVBVGVEVFOU
    TVBVGLZLVEVFUTVDFEZVEVFUDVLAVDUEVGVBVMVGVBVMVGVBVBVMUDVGVBLZCVDFVNVDCVGVBVD
    CCHIZCBCCHUFVBCDEVOCNCUGCUHUIUJUKULTUMUNUOSUPUQUCURUCUS $.
    $( [29-Jul-2004] $) $( [26-Jun-2004] $)

  $( The Hilbert lattice satisfies the atom exchange property.  Proposition
     1(i) of [Kalmbach] p. 140.  A version of this theorem related to vector
     analysis was originally proved by Hermann Grassmann in 1862.  Also
     Definition 3.4-3(b) in [MegillPavicic] p. 2345 (PDF p. 8) (use ~ atnem0
     to obtain atom inequality). $)
  atexcht $p |- ( ( A e. CH /\ B e. Atoms /\ C e. Atoms ) ->
            ( ( B (_ ( A vH C ) /\ ( A i^i B ) = 0H ) -> C (_ ( A vH B ) ) ) $=
    ( cch wcel cat w3a chj co wss cin c0h wceq wa chub2t ancoms atelch sylan2
    3adant2 adantr wpss wi ccv wbr cvp chjclt cvpsst syldan sylbid 3adant3
    adantld chub1t a1d ancrd wb chlubt syl3dan3 sylibd id syl3an adantrd jcad
    imp anim12d ancomsd psssstr syl6 chcv2t 3simp1 3jca cvnbtwn2t syl syld mpd
    sseqtr4d ex ) ADEZBFEZCFEZGZBACHIZJZABKLMZNZCABHIZJVTWDNZCWAWEVTCWAJZWDVQVS
    WGVRVQCDEZWGVSWHVQWGCAOPCQZRSTWFAWEUAZWEWAJZNZWEWAMZVTWDWLVTWDWJWKVTWCWJWBV
    QVRWCWJUBVSVQVRNWCAWEUCUDZWJABUEVQVRWEDEZWNWJUBVQBDEZWOVRABUFZBQZRAWEUGUHUI
    UJZUKVTWBWKWCVQWPWHWBWKUBVQVRVSVQWPWHGZWBAWAJZWBNZWKWTWBXAWTXAWBVQWHXAWPACU
    LSUMUNVQWPWADEZXBWKUOWHABWAUPVQWHXCWPACUFSZUQURVQUSZWRWIUTZVAVBVCVTWDWLWMUB
    ZVTWDAWAUCUDZXGVTWDAWAUAZXHVTWDWLXIVTWCWBWLVTWCWJWBWKWSXFVDVEAWEWAVFVGVQVSX
    IXHUOVRACVHSURVTVQXCWOGZXHXGUBVQWPWHXJVQVRVSWTVQXCWOVQWPWHVIXDVQWPWOWHWQUJV
    JXEWRWIUTAWAWEVKVLVMVCVNVOVP $.
    $( [27-Jul-2004] $) $( [27-Jun-2004] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    atoml.1 $e |- A e. CH $.
    $( An assertion holding in atomic orthomodular lattices that is equivalent
       to the exchange axiom.  Proposition 3.2.17 of [PtakPulmannova] p. 66. $)
    atoml $p |- ( B e. Atoms ->
                ( ( A vH B ) i^i ( _|_ ` A ) ) e. ( Atoms u. { 0H } ) ) $=
      ( vx cat wcel chj co cort cfv cin c0h wceq wo csn cun wne wa cv wss wrex
      cch wi atelch chjclt mpan syl choccl chinclt mpan2 hatomict ex 3syl imp
      wb pjoml3t pjococ opreq1i ineq1i incom eqtr3 syl5eq inss2 sstr syl2an
      ad2ant2lr chub1t adantr chlubt mp3an1 sylan2 biimpd ancoms mpand inss1
      adantrr exp3a com23 anim12i atexcht pm3.22 adantl csh chsh chshi orthin
      jca sylc ad2antlr eqssd ineq1d eqtr3d eleq1d exp43 com24 imp31 ibd
      r19.23adv mpd necon1bd orrd elun fvex inex2 elsnc orbi2i bitr sylibr ) BE
      FZABGHZAIJZKZEFZXLLMZNZXLELOZPFZXIXMXNXIXMXLLXIXLLQZXMXIXRRZDSZXLTZDEUAZX
      MXIXRYBXIXJUBFZXLUBFZXRYBUCXIBUBFZYCBUDZAUBFZYEYCCABUEUFZUGYCXKUBFZYDACUH
      ZXJXKUIUJYDXRYBDXLUKULUMUNXSYAXMDEXSYAXTEFZXMXSYAYKXMUCXSYARYKXMXIXRYAYKY
      KXMUOZUCXIYKYAXRYLXIYKYAXRYLXIYKRZYAXRRZRZXTXLEYOAXTGHZXKKZXTXLYKYAYQXTMZ
      XIXRXTUBFZXTXKTZYRYKYAYSYTRZXKXKIJZXTGHZKZXTYQYSYTUUDXTMZYIYSYTUUEUCYJXKX
      TUPUFUNUUCXKKYQUUDUUCYPXKUUBAXTGACUQURUSUUCXKUTVAVBXTUDZYAXLXKTYTXJXKVCXT
      XLXKVDUJZVEVFYOYPXJXKYOYPXJYMYAYPXJTZXRYMXTXJTZUUHYAYMUUIUUHYEYSUUIUUHUCX
      IYKYEYSRAXJTZUUIUUHYEUUJYSYGYEUUJCABVGUFVHYSYEUUJUUIRZUUHUCYSYERUUKUUHYSY
      CUUKUUHUOZYEYGYSYCUULCAXTXJVIVJYHVKVLVMVNYFUUFVEUNYAXLXJTUUIXJXKVOXTXLXJV
      DUJZVKVPYEYPUBFZRZBYPTZRAYPTZXJYPTZYOUUOUUPUUQUURUCUUOUUQUUPUURUUOUUQUUPU
      URUUOUUQUUPRZUURYGYEUUNUUSUURUOCABYPVIVJVLVQVRUNYOUUOUUPYMUUOYNXIYEYKUUNY
      FYKYSUUNUUFYGYSUUNCAXTUEUFUGVSVHYKXIRZUUIAXTKZLMZRZUUPYOYGYKXIUVCUUPUCCAX
      TBVTVJYMUUTYNXIYKWAVHYKYAUVCXIXRYKYARUUIUVBYAUUIYKUUMWBYSYTUVBYKYAUUAXTAK
      ZLUVAYSYTUVDLMZYSXTWCFZYTUVEUCZXTWDUVFAWCFUVGACWEXTAWFUJUGUNAXTUTVBUUFUUG
      VEWGVFWHWGYKUUQXIYNYKYSUUQUUFYGYSUUQCAXTVGUFUGWIWHWJWKWLWMWNWOWPWQULVRWRW
      SULWTXAXQXMXLXPFZNXOXLEXPXBUVHXNXMXLLXKXJAIXCXDXEXFXGXH $.
      $( [9-May-2005] $) $( [24-Jun-2004] $)

    $( An assertion holding in atomic orthomodular lattices that is equivalent
       to the exchange axiom.  Proposition P8(ii) of [BeltramettiCassinelli1]
       p. 400. $)
    atoml2 $p |- ( ( B e. Atoms /\ -. B (_ A ) ->
                ( ( A vH B ) i^i ( _|_ ` A ) ) e. Atoms ) $=
      ( cat wcel wss wn chj co cort cfv cin c0h wceq cch atelch pjoml5t mpan
      syl incom eqeq1i biimp opreq2d chj0 syl6eq sylan9req ex wb chlejb2t
      mpan2 sylibrd con3d csn cun wo atoml elun h0elch elisseti elsnc2 orbi2i
      orcom 3bitr sylib ord syld imp ) BDEZBAFZGZABHIZAJKZLZDEZVHVJVMMNZGVNVHVO
      VIVHVOVKANZVIVHVOVPVHVOVKAVLVKLZHIZAVHBOEZVRVKNZBPZAOEZVSVTCABQRSVOVRAMHI
      AVOVQMAHVOVQMNVMVQMVKVLTUAUBUCACUDUEUFUGVHVSVIVPUHZWAVSWBWCCBAUIUJSUKULVH
      VOVNVHVMDMUMZUNEZVOVNUOZABCUPWEVNVMWDEZUOVNVOUOWFVMDWDUQWGVOVNVMMMOURUSUT
      VAVNVOVBVCVDVEVFVG $.
      $( [2-Jul-2006] $) $( [12-Jun-2006] $)

    $( An ordering law for a Hilbert lattice atom and a commuting subspace. $)
    atord $p |- ( ( B e. Atoms /\ A C_H B ) ->
                ( B (_ A \/ B (_ ( _|_ ` A ) ) ) $=
      ( cat wcel ccm wbr wa wss cort cfv wn cin chj co wceq cch c0h choccl
      chinclt mpan chj0t syl incom syl6eq h0elch chjcomt mpan2 eqtr3d chocin
      eqtr opreq1i syl6eqr adantr w3a cmid cmcm2i fh2t mpanr1 mp3anl2 mpanl1
      eqtr4d atelch sylan atoml2 adantlr eqeltrd wb atssmat ad2antrr mpbird ex
      orrd ) BDEZABFGZHZBAIZBAJKZIZVPVQLZVSVPVTHZVSBVRMZDEZWAWBABNOZVRMZDVPWBWE
      PVTVPWBVRWDMZWEBQEZVOWBWFPVNWGVOHWBVRAMZVRBMZNOZWFWGWBWJPVOWGWBRWINOZWJWG
      WIRNOZWBWKWGWLWIWBWGWIQEZWLWIPVRQEZWGWMACSZVRBTUAZWIUBUCVRBUDUEWGWMWLWKPZ
      WPWMRQEWQUFWIRUGUHUCUIWHRWINWHAVRMRVRAUDACUJUKULUMUNWNWGVOWFWJPZWOWNAQEZW
      GVOWRCWNWSWGUOAVRFGVOWRAACCACUPUQVRABURUSUTVAVBBVCVDVRWDUDUEUNVNVTWEDEVOA
      BCVEVFVGVNVSWCVHZVOVTVNWNWTWOBVRVIUHVJVKVLVM $.
      $( [4-Jul-2006] $) $( [12-Jun-2006] $)

    $( Lemma for ~ atcvat . $)
    atcvatlem $p |- ( ( ( B e. Atoms /\ C e. Atoms ) /\
        ( A =/= 0H /\ A (. ( B vH C ) ) ) -> ( -. B (_ A -> A e. Atoms ) ) $=
      ( vx cat wcel wa c0h wne chj co wpss wss wn wi cv wrex ccv wbr wceq cin
      atnem0 sseq1 biimpcd con3d imp adantrl syl5bi cch wb cvp chjcomt atelch
      sylan2 breq2d bitrd sylan sylibd ancoms adantlr w3a chub1t syl2an
      3adant3 adantr sstr pssss adantl incom syl5eq adantrr atexcht syl3an1
      mp2and jca 3simp1 3simp3 chjclt 3jca syl3an chlubt syl mpbid 3adant2
      anim12i syl3dan3 syl3anl eqssd anassrs psseq2d ex ibd exp32 3expa an1rs
      com34 imp45 pm3.27 cvnbtwn3t mp3an3 exp4a com23 imp4a eleq1d biimprcd
      exp4c pm2.43b exp4d r19.23adva hatomic syl5 imp32 ) BFGZCFGZHZAIJZABCKLZM
      ZBANZOZAFGZPZXPEQZANZEFRXSYCPZXQXPYEYFEFXPYDFGZHZYEXSYAYBXPYGYEXSYAHZHZYB
      PZXPYGYKYGXPYGYJYBYHYJHZYBYGYLAYDFYLYDBYDKLZSTZAYMMZAYDUAZYHYJYNXNYGYJYNP
      ZXOYGXNYQYGXNHZYJYDBUBZIUAZYNYRYDBUAZOZYTYJYDBUCZYEYAUUBXSYEYAUUBYEUUAXTU
      UAYEXTYDBAUDUEUFUGZUHUIYDUJGZXNYTYNUKYGUUEXNHZYTYDYDBKLZSTYNYDBULUUFUUGYM
      YDSUUEBUJGZUUGYMUAXNYDBUMBUNZUOUPUQYDUNZURUSUTVAUGYHYEXSYAYOYHYEYAXSYOXNY
      GXOYEYAXSYOPZPPZXNYGXOUULXNYGXOVBZYEYAUUKUUMYEYAHZHZXSYOUUOXSXSYOUKUUOXSH
      XRYMAUUMUUNXSXRYMUAUUMUUNXSHZHZXRYMUUQBYMNZCYMNZHZXRYMNZUUQUURUUSUUMUURUU
      PXNYGUURXOUUHUUEUURXNYGBYDVCUUIUUJVDVEVFUUQYDXRNZBYDUBZIUAZUUSUUPUVBUUMYE
      XSUVBYAYEAXRNUVBXSYDAXRVGAXRVHUOVAZVIUUMUUNUVDXSUUOYSIUVCUUMUUNYTXNYGUUNY
      TPZXOYGXNUVFYRUUBYTUUNUUCUUDUIUTVEUGBYDVJVKVLUUMUVBUVDHUUSPZUUPUUHYGXOUVG
      XNBYDCVMUUIVNVFVOVPUUMUUTUVAUKZUUPUUMUUHCUJGZYMUJGZVBZUVHUUHUUEUVIUVKXNYG
      XOUUHUUEUVIVBZUUHUVIUVJUUHUUEUVIVQUUHUUEUVIVRUUHUUEUVJUVIBYDVSZVEVTUUIUUJ
      CUNZWABCYMWBWCVFWDUUHUUEUVIUUPYMXRNZXNYGXOUVLUUPHBXRNZUVBHZUVOUVLUVPUUPUV
      BUUHUVIUVPUUEBCVCWEUVEWFUVLUVQUVOUKZUUPUUHUUEXRUJGZUVRUVIBYDXRWBUUHUVIUVS
      UUEBCVSWEWGVFWDUUIUUJUVNWHWIWJWKWLWMWNWOWPWQWRYHYEYNYOHYPPZYIYHYEUVTXNYGY
      EUVTPZXOXNYGHUUEUVJHZUWAUUHUUEUWBXNYGUUHUUEHUUEUVJUUHUUEWSUVMVPUUIUUJVDUW
      BYEYNYOYPUWBYNYEYOYPPUWBYNYEYOYPUUEUVJAUJGYNYEYOHYPPPDYDYMAWTXAXBXCXDWCVA
      UGVLVOXEXFXGXHUGXIXJEADXKXLXM $.
      $( [23-Mar-2007] $) $( [27-Jun-2004] $)

    $( A nonzero Hilbert lattice element less than the join of two atoms
       is an atom. $)
    atcvat $p |- ( ( B e. Atoms /\ C e. Atoms ) ->
                 ( ( A =/= 0H /\ A (. ( B vH C ) ) -> A e. Atoms ) ) $=
      ( cat wcel wa c0h wne chj co wpss wss wn wo cch wi w3a wb chlubt 3comr
      ssnpss syl6bi con2d ianor syl6ib mp3an1 atelch syl2an imp adantrl
      atcvatlem wceq chjcomt psseq2d anbi2d ex sylbird ancoms jaod mpd ) BEFZCE
      FZGZAHIZABCJKZLZGZAEFZVDVHGZBAMZNZCAMZNZOZVIVDVGVOVEVDVGVOBPFZCPFZVGVOQZV
      BVCAPFZVPVQVRDVSVPVQRZVGVKVMGZNVOVTWAVGVTWAVFAMZVGNVPVQVSWAWBSBCATUAVFAUB
      UCUDVKVMUEUFUGBUHZCUHZUIUJUKVJVLVIVNABCDULVDVHVNVIQZVCVBVHWEQVCVBGZVHVEAC
      BJKZLZGZWEWFWHVGVEWFWGVFAVQVPWGVFUMVCVBCBUNWDWCUIUOUPWFWIWEACBDULUQURUSUJ
      UTVAUQ $.
      $( [25-Mar-2007] $) $( [28-Jun-2004] $)

    $( A Hilbert lattice element covered by the join of two distinct atoms
       is an atom. $)
    atcvat2 $p |- ( ( B e. Atoms /\ C e. Atoms ) ->
                 ( ( -. B = C /\ A <o ( B vH C ) ) -> A e. Atoms ) ) $=
      ( cat wcel wa wceq wn chj co ccv wbr wi c0h wne cch wb atcv1t mp3anl1
      necon3abid wpss chjclt atelch syl2an cvpsst mpan syl atcvat exp3a com23
      syld imp sylbird ex imp3a ) BEFZCEFZGZBCHZIZABCJKZLMZAEFZUSVCVAVDUSVCVAVD
      NUSVCGZVAAOPZVDVEUTAOAQFZUQURVCAOHUTRDABCSTUAUSVCVFVDNZUSVCAVBUBZVHUSVBQF
      ZVCVINZBQFCQFVJUQURBCUCBUDCUDUEVGVJVKDAVBUFUGUHUSVFVIVDUSVFVIVDABCDUIUJUK
      ULUMUNUOUKUP $.
      $( [29-Jul-2004] $) $( [26-Jun-2004] $)
  $}

  $( An ordering law for a Hilbert lattice atom and a commuting subspace. $)
  atordt $p |- ( ( A e. CH /\ B e. Atoms /\ A C_H B ) ->
              ( B (_ A \/ B (_ ( _|_ ` A ) ) ) $=
    ( cch wcel cat ccm wbr wss cort cfv wo wa wi c0h cif wceq breq1 anbi2d
    sseq2 fveq2 sseq2d orbi12d imbi12d h0elch elimel atord dedth 3impib ) ACDZB
    EDZABFGZBAHZBAIJZHZKZUIUJUKLZUOMUJUIANOZBFGZLZBUQHZBUQIJZHZKZMANAUQPZUPUSUO
    VCVDUKURUJAUQBFQRVDULUTUNVBAUQBSVDUMVABAUQITUAUBUCUQBANCUDUEUFUGUH $.
    $( [7-Jul-2006] $) $( [12-Jun-2006] $)

  $( A Hilbert lattice element covered by the join of two distinct atoms
     is an atom. $)
  atcvat2t $p |- ( ( A e. CH /\ B e. Atoms /\ C e. Atoms ) ->
               ( ( -. B = C /\ A <o ( B vH C ) ) -> A e. Atoms ) ) $=
    ( cch wcel cat wceq wn chj co ccv wbr wa wi c0h cif breq1 anbi2d eleq1
    imbi12d imbi2d h0elch elimel atcvat2 dedth 3impib ) ADEZBFEZCFEZBCGHZABCIJZ
    KLZMZAFEZNZUGUHUIMZUONUPUJUGAOPZUKKLZMZUQFEZNZNAOAUQGZUOVAUPVBUMUSUNUTVBULU
    RUJAUQUKKQRAUQFSTUAUQBCAODUBUCUDUEUF $.
    $( [29-Jul-2004] $) $( [29-Jun-2004] $)

