$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Atoms, exchange and covering properties, atomicity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.
    $( The Hilbert lattice has the covering property.  Proposition 1(ii) of
       [Kalmbach] p. 140 (and its converse). $)
    chcv1 $p |- ( ( A e. CH /\ B e. Atoms ) ->
                ( -. B C_ A <-> A <o ( A vH B ) ) ) $=
      ( vx cch wcel cat wa wss wn chj co ccv wbr cv c0v wne csn cspn cfv wceq
      chil wrex wi sseq1 notbid opreq2 breq2d imbi12d spansncv2 syl5cbir
      adantld r19.23adva imp atom1d sylan2b wpss chjcl cvpss syldan chnle
      sylibrd atelch sylan2 impbid ) ADEZBFEZGBAHZIZAABJKZLMZVECNZOPZBVKQRSZTZG
      ZCUAUBZVHVJUCZVFVEVPVQVEVOVQCUAVEVKUAEGZVNVQVLVNVQVMAHZIZAAVMJKZLMZUCVRVN
      VHVTVJWBVNVGVSBVMAUDUEVNVIWAALBVMAJUFUGUHAVKUIUJUKULUMCBUNUOVEBDEZVJVHUCV
      FVEWCGVJAVIUPZVHVEWCVIDEVJWDUCABUQAVIURUSABUTVABVBVCVD $.
      $( [11-Jun-2004] $)

    $( The Hilbert lattice has the covering property. $)
    chcv2 $p |- ( ( A e. CH /\ B e. Atoms ) ->
                ( A C. ( A vH B ) <-> A <o ( A vH B ) ) ) $=
      ( cch wcel cat wa wss wn chj co wpss ccv wbr wb chnle atelch sylan2
      chcv1 bitr3d ) ACDZBEDZFBAGHZAABIJZKZAUCLMTBCDUBUDNUAABOBPQABRS $.
      $( [11-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The join of a closed subspace and an atom equals their subspace sum.
       Special case of remark in [Kalmbach] p. 65, stating that if ` A ` or
       ` B ` is finite dimensional, then this equality holds. $)
    chjatom $p |- ( ( A e. CH /\ B e. Atoms ) -> ( A +H B ) = ( A vH B ) ) $=
      ( vx cch wcel cat cph co chj wceq cv c0v wne csn cspn cfv wa chil wrex
      wi opreq2 eqeq12d spansnj syl5bir exp3a adantl com3l r19.23adv atom1d
      syl5ib imp ) ADEZBFEZABGHZABIHZJZULCKZLMZBUQNOPZJZQZCRSUPUMULVAUPCRVAULUQ
      REZUPUTULVBUPTTURUTULVBUPUTUPAUSGHZAUSIHZJULVBQUTUNVCUOVDBUSAGUABUSAIUAUB
      AUQUCUDUEUFUGUHCBUIUJUK $.
      $( [27-Feb-2005] $) $( [4-Jun-2004] $)
  $}

  ${
    $d x y A $.
    shatomic.1 $e |- A e. SH $.
    $( The lattice of Hilbert subspaces is atomic, i.e. any nonzero element is
       greater than or equal to some atom.  Part of proof of Theorem 16.9 of
       [MaedaMaeda] p. 70. $)
    shatomici $p |- ( A =/= 0H -> E. x e. Atoms x C_ A ) $=
      ( vy c0h wne cv c0v wrex wss cat shne0i csn cort cfv wcel wa sseq1
      rcla4ev chil h1da sheli sylan csh sh1dle mpan adantr sylanc r19.23aiva
      sylbi ) BEFDGZHFZDBIAGZBJZAKIZDBCLULUODBUKMNONOZKPZUPBJZUOUKBPZULQUNURAUP
      KUMUPBRSUKTPULUQUSUKUAUKBCUBUCUSURULBUDPUSURCBUKUEUFUGUHUIUJ $.
      $( [30-Jun-2006] $) $( [24-Nov-2004] $)
  $}

  ${
    $d x y A $.
    hatomic.1 $e |- A e. CH $.
    $( The Hilbert lattice is atomic, i.e. any nonzero element is greater than
       or equal to some atom.  Remark in [Kalmbach] p. 140. $)
    hatomici $p |- ( A =/= 0H -> E. x e. Atoms x C_ A ) $=
      ( chshii shatomici ) ABBCDE $.
      $( [2-Jul-2006] $) $( [22-Jul-2001] $)
  $}

  ${
    $d x y A $.
    $( A Hilbert lattice is atomic, i.e. any nonzero element is greater than or
       equal to some atom.  Remark in [Kalmbach] p. 140.  Also Definition 3.4-2
       in [MegillPavicic] p. 2345 (PDF p. 8). $)
    hatomic $p |- ( ( A e. CH /\ A =/= 0H ) -> E. x e. Atoms x C_ A ) $=
      ( cch wcel c0h wne cv wss cat wrex wi cif wceq neeq1 sseq2 rexbidv
      imbi12d h0elch elimel hatomici dedth imp ) BCDZBEFZAGZBHZAIJZUCUDUGKUCBEL
      ZEFZUEUHHZAIJZKBEBUHMZUDUIUGUKBUHENULUFUJAIBUHUEOPQAUHBECRSTUAUB $.
      $( [7-Oct-2006] $) $( [24-Jun-2004] $)
  $}

  ${
    $d x y A $.
    shatomistic.1 $e |- A e. SH $.
    $( The lattice of Hilbert subspaces is atomistic, i.e. any element is the
       supremum of its atoms.  Part of proof of Theorem 16.9 of [MaedaMaeda]
       p. 70. $)
    shatomistici $p |- A = ( span ` U. { x e. Atoms | x C_ A } ) $=
      ( vy cv wss cat crab cuni cspn cfv wcel c0v eleq1 wne wa csn wceq chil
      csh sheli spansnsh spanid 3syl adantr spansna sylan spansnss mpan jca
      sseq1 elrab sylibr elssuni cch atssch chsssh sstri rabss2 ax-mp uniss
      unimax shssii eqsstri spanss eqsstr3d spansnid syl sseldd spancl sh0 a1i
      pm2.61ne ssriv mp2an fveq2i eqtri sseqtri eqssi ) BAEZBFZAGHZIZJKZDBWDDEZ
      BLZWEWDLMWDLZWEMWEMWDNWFWEMOZPZWEQJKZWDWEWIWJWJJKZWDWFWKWJRZWHWFWESLZWJTL
      WLWEBCUAZWEUBWJUCUDUEWIWJWBLZWJWCFZWKWDFZWIWJGLZWJBFZPWOWIWRWSWMWHWRWFWEU
      FWNUGWFWSWHBTLZWFWSCBWEUHUIUEUJWAWSAWJGVTWJBUKULUMWJWBUNWCSFZWPWQWCWAATHZ
      IZSWBXBFZWCXCFZGTFXDGUOTUPUQURWAAGTUSUTWBXBVAUTZXCBSWTXCBRCABTVBUTZBCVCVD
      ZURZWJWCVEUIUDVFWFWEWJLZWHWFWMXJWNWEVGVHUEVIWGWFWDTLZWGXAXKXIWCVJUTWDVKUT
      VLVMVNWDXCJKZBXCSFXEWDXLFXHXFWCXCVEVOXLBJKZBXCBJXGVPWTXMBRCBUCUTVQVRVS $.
      $( [26-Nov-2004] $)
  $}

  ${
    $d x y A $.
    hatomistic.1 $e |- A e. CH $.
    $( ` CH ` is atomistic, i.e. any element is the supremum of its atoms.
       Remark in [Kalmbach] p. 140. $)
    hatomistici $p |- A = ( \/H ` { x e. Atoms | x C_ A } ) $=
      ( vy cv wss cat crab chsup cfv cch wcel ssrab2 atssch sstri chsupcl
      ax-mp chshii wa atelch anim1i sseq1 elrab 3imtr4i ssriv wi chsupss mp2an
      wceq chsupid sseqtri cort cin wrex wn c0h cuni elssuni sylbir chsupunss
      sstr2 mpi syl ex wne atn0 adantr wb chle0 ssin chocini sseq2i bitr2i
      syl5bbr biimpa expr necon3ad mpd syld imnan sylib notbii nrex choccli
      chincli hatomici necon1bi omlsii eqcomi ) AEZBFZAGHZIJZBWMBWLKFZWMKLWLGKW
      KAGMNOZWLPQZBCRWMWKAKHZIJZBWLWQFZWMWRFZDWLWQDEZGLZXABFZSZXAKLZXCSXAWLLZXA
      WQLXBXEXCXATZUAWKXCAXAGWJXABUBZUCZWKXCAXAKXHUCUDUEWNWQKFWSWTUFWOWKAKMWLWQ
      UGUHQBKLWRBUICABUJQUKXABWMULJZUMZFZDGUNZUOXKUPUIXLDGXBXCXAXJFZSZUOZXLUOXB
      XCXNUOZUFXPXBXCXAWMFZXQXBXCXRXDXAWLUQZFZXRXDXFXTXIXAWLURUSXTXSWMFZXRWNYAW
      OWLUTQXAXSWMVAVBVCVDXBXRXQXBXRSZXAUPVEZXQXBYCXRXAVFVGYBXNXAUPXBXRXNXAUPUI
      ZXBXRXNSZYDXBXAUPFZYDYEXBXEYFYDVHXGXAVIVCYEXAWMXJUMZFYFXAWMXJVJYGUPXAWMWP
      VKVLVMVNVOVPVQVRVDVSXCXNVTWAXOXLXABXJVJWBWAWCXMXKUPDXKBXJCWMWPWDWEWFWGQWH
      WI $.
      $( [14-Aug-2002] $)
  $}

  ${
    $d x A $.  $d x B $.
    chpssat.1 $e |- A e. CH $.
    chpssat.2 $e |- B e. CH $.
    $( Two Hilbert lattice elements in a proper subset relationship imply the
       existence of an atom less than or equal to one but not the other. $)
    chpssati $p |- ( A C. B -> E. x e. Atoms ( x C_ B /\ -. x C_ A ) ) $=
      ( wpss wss wn cv wa cat wrex dfpss3 pm3.27bi wral wi iman ralbii crab
      ss2rab chsup cfv cch ssrab2 atssch sstri chsupss mp2an hatomistici
      3sstr4g sylbir con3i dfrex2 sylibr syl ) BCFZCBGZHZAIZCGZUSBGZHJZAKLZUPBC
      GURBCMNURVBHZAKOZHVCVEUQVEUTVAPZAKOZUQVFVDAKUTVAQRVGUTAKSZVAAKSZGZUQUTVAA
      KTVJVHUAUBZVIUAUBZCBVHUCGVIUCGVJVKVLGPVHKUCUTAKUDUEUFVIKUCVAAKUDUEUFVHVIU
      GUHACEUIABDUIUJUKUKULVBAKUMUNUO $.
      $( [10-Jun-2004] $)

    $( The Hilbert lattice is relatively atomic.  Remark 2 of [Kalmbach]
       p. 149. $)
    chrelati $p |- ( A C. B ->
                  E. x e. Atoms ( A C. ( A vH x ) /\ ( A vH x ) C_ B ) ) $=
      ( wpss cv wss wn wa cat wrex chj co chpssati wcel cch wb chnle mpan
      adantl ibar chlub mp3an13 sylan9bb anbi12d pssss atelch syl2an ancom
      syl5bb rexbidva mpbid ) BCFZAGZCHZUOBHIZJZAKLBBUOMNZFZUSCHZJZAKLABCDEOUNU
      RVBAKUNUOKPZJUQUPJZVBURBCHZUOQPZVDVBRUNVCVEVFJUQUTUPVAVFUQUTRZVEBQPZVFVGD
      BUOSTUAVEUPVEUPJZVFVAVEUPUBVHVFCQPVIVARDEBUOCUCUDUEUFBCUGUOUHUIUPUQUJUKUL
      UM $.
      $( [11-Jun-2004] $)

    $( A consequence of relative atomicity. $)
    chrelat2i $p |- ( -. A C_ B <-> E. x e. Atoms ( x C_ A /\ -. x C_ B ) ) $=
      ( wss wn cv wa cat wrex cin wpss nssinpss chj co chincli chrelati wcel
      cch wi atelch wb chlub mp3an13 pm3.27 syl6bir adantld chnle mpan ssin
      notbii syl5bb anbi12d pm3.21 wo ianor notnot orbi1i orcom imor bitr4i
      3bitr2ri sylib con2i adantrl jcad syl reximia sylbi wral sstr2 com12 a1d
      r19.21aiv iman ralbii ralnex bitri impbii ) BCFZGZAHZBFZWCCFZGZIZAJKZWBBC
      LZBMZWHBCNWJWIWIWCOPZMZWKBFZIZAJKWHAWIBBCDEQZDRWNWGAJWCJSZWCTSZWNWGUAWCUB
      WQWNWDWFWQWMWDWLWQWMWIBFZWDIZWDWITSZWQBTSWSWMUCWODWIWCBUDUEZWRWDUFUGUHWQW
      NWDWEIZGZWSIWFWQXCWLWSWMWQWCWIFZGZWLXCWTWQXEWLUCWOWIWCUIUJXBXDWCBCUKULUMX
      AUNXCWDWFWRWEXCWDIZWEWDXBUAZXFGZWEWDUOXHXCGZWDGZUPXBXJUPZXGXCWDUQXBXIXJXB
      URUSXKXJXBUPXGXBXJUTWDXBVAVBVCVDVEVFUGVGVHVIVHVJWAWHWAWDWEUAZAJVKZWHGZWAX
      LAJWAXLWPWDWAWEWCBCVLVMVNVOXMWGGZAJVKXNXLXOAJWDWEVPVQWGAJVRVSVDVEVT $.
      $( [30-Jun-2004] $)

    $( If a Hilbert lattice element covers another, it equals the other joined
       with some atom.  This is a consequence of the relative atomicity of
       Hilbert space. $)
    cvati $p |- ( A <o B -> E. x e. Atoms ( A vH x ) = B ) $=
      ( ccv wbr cv chj co wpss wss wa cat wrex wceq cch wcel wi cvpss mp2an
      chrelati syl atelch chjcl mpan cvnbtwn2 mp3an12 com12 reximdvai mpd ) BCF
      GZBBAHZIJZKUNCLMZANOZUNCPZANOULBCKZUPBQRZCQRZULURSDEBCTUAABCDEUBUCULUOUQA
      NUMNRZULUOUQSZVAUNQRZULVBSZVAUMQRZVCUMUDUSVEVCDBUMUEUFUCUSUTVCVDDEBCUNUGU
      HUCUIUJUK $.
      $( [30-Nov-2004] $)

    $( An alternate way to express the covering property. $)
    cvbr3i $p |- ( A <o B <-> ( A C. B /\ E. x e. Atoms ( A vH x ) = B ) ) $=
      ( ccv wbr wpss cv chj co wceq cat wrex wa cch wcel wi cvpss mp2an cvati
      jca wb chcv2 mpan adantr psseq2 adantl breq2 3bitr3d biimpd ex com3r
      r19.23adv imp impbii ) BCFGZBCHZBAIZJKZCLZAMNZOUQURVBBPQZCPQUQURRDEBCSTAB
      CDEUAUBURVBUQURVAUQAMUSMQZVAURUQVDVAURUQRVDVAOZURUQVEBUTHZBUTFGZURUQVDVFV
      GUCZVAVCVDVHDBUSUDUEUFVAVFURUCVDUTCBUGUHVAVGUQUCVDUTCBFUIUHUJUKULUMUNUOUP
      $.
      $( [30-Nov-2004] $)

    $( Lemma for ~ cvexchi . $)
    cvexchlem $p |- ( ( A i^i B ) <o B -> A <o ( A vH B ) ) $=
      ( vx cv wss cin wn wa cat wrex ccv wbr chj co wpss cch wcel wi chincli
      cvpss mp2an chpssati syl wb chcv1 mpan biimpa ssin ancom bitr3i baibr
      notbid biimpar sylan2 adantrr wceq chjass mp3an12 chabs1i opreq1i
      syl5reqr adantr chnle chlub mp3an13 inss2 biantrur syl5bb anbi12d chjcl
      cvnbtwn2 com23 sylbid imp32 opreq2d eqtr3d atelch sylan breqtrd exp32
      r19.23aiv mpcom ) EFZBGZWEABHZGZIZJZEKLZWGBMNZAABOPZMNZWLWGBQZWKWGRSZBRSZ
      WLWOTABCDUAZDWGBUBUCEWGBWRDUDUEWJWLWNTEKWEKSZWJWLWNWSWJWLJZJAAWEOPZWMMWSW
      JAXAMNZWLWSWEAGZIZXBWJWSXDXBARSZWSXDXBUFCAWEUGUHUIWFXDWIWFXCWHWHWFXCWHXCW
      FJWFXCJWEABUJXCWFUKULUMUNUOUPUQWERSZWTXAWMURWSXFWTJZAWGWEOPZOPZXAWMXFXIXA
      URWTXFAWGOPZWEOPZXIXAXEWPXFXKXIURCWRAWGWEUSUTXJAWEOABCDVAVBVCVDXGXHBAOXFW
      JWLXHBURZXFWJWGXHQZXHBGZJZWLXLTXFWIWFJXOWJXFWIXMWFXNWPXFWIXMUFWRWGWEVEUHX
      FWGBGZWFJZXNWFWPXFWQXQXNUFWRDWGWEBVFVGXPWFABVHVIVJVKWFWIUKVJXFWLXOXLXFXHR
      SZWLXOXLTTZWPXFXRWRWGWEVLUHWPWQXRXSWRDWGBXHVMUTUEVNVOVPVQVRWEVSVTWAWBWCWD
      $.
      $( [10-Jun-2004] $)

    $( The Hilbert lattice satisfies the exchange axiom.  Proposition 1(iii) of
       [Kalmbach] p. 140 and its converse.  Originally proved by Garrett
       Birkhoff in 1933. $)
    cvexchi $p |- ( ( A i^i B ) <o B <-> A <o ( A vH B ) ) $=
      ( cin ccv wbr chj co cvexchlem cort cfv choccli chdmj1i incom eqtri
      breq1i chdmm1i chjcomi breq2i 3imtr4i cch wcel wb chjcli cvcon3 mp2an
      chincli impbii ) ABEZBFGZAABHIZFGZABCDJULKLZAKLZFGZBKLZUJKLZFGZUMUKUQUOEZ
      UOFGUQUQUOHIZFGUPUSUQUOBDMZACMZJUNUTUOFUNUOUQEUTABCDNUOUQOPQURVAUQFURUOUQ
      HIVAABCDRUOUQVCVBSPTUAAUBUCULUBUCUMUPUDCABCDUEAULUFUGUJUBUCBUBUCUKUSUDABC
      DUHDUJBUFUGUAUI $.
      $( [12-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A consequence of relative atomicity. $)
    chrelat2 $p |- ( ( A e. CH /\ B e. CH ) ->
                  ( -. A C_ B <-> E. x e. Atoms ( x C_ A /\ -. x C_ B ) ) ) $=
      ( cch wcel wss wn cv wa cat wrex wb chil cif wceq sseq1 notbid sseq2
      anbi1d rexbidv bibi12d anbi2d helch elimel chrelat2i dedth2h ) BDEZCDEZBC
      FZGZAHZBFZUKCFZGZIZAJKZLUGBMNZCFZGZUKUQFZUNIZAJKZLUQUHCMNZFZGZUTUKVCFZGZI
      ZAJKZLBCMMBUQOZUJUSUPVBVJUIURBUQCPQVJUOVAAJVJULUTUNBUQUKRSTUACVCOZUSVEVBV
      IVKURVDCVCUQRQVKVAVHAJVKUNVGUTVKUMVFCVCUKRQUBTUAAUQVCBMDUCUDCMDUCUDUEUF
      $.
      $( [1-Jul-2004] $)

    $( A consequence of relative atomicity. $)
    chrelat3 $p |- ( ( A e. CH /\ B e. CH ) ->
                   ( A C_ B <-> A. x e. Atoms ( x C_ A -> x C_ B ) ) ) $=
      ( cch wcel wa wss cv wn cat wral wi wrex chrelat2 dfrex2 syl6bb con4bid
      iman ralbii syl6bbr ) BDECDEFZBCGZAHZBGZUCCGZIFZIZAJKZUDUELZAJKUAUBUHUAUB
      IUFAJMUHIABCNUFAJOPQUIUGAJUDUERST $.
      $( [2-Jul-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    chrelat3.1 $e |- A e. CH $.
    chrelat3.2 $e |- B e. CH $.
    $( A consequence of the relative atomicity of Hilbert space: the ordering
       of Hilbert lattice elements is completely determined by the atoms they
       majorize. $)
    chrelat3i $p |- ( A C_ B <-> A. x e. Atoms ( x C_ A -> x C_ B ) ) $=
      ( cch wcel wss cv wi cat wral wb chrelat3 mp2an ) BFGCFGBCHAIZBHPCHJAKLMD
      EABCNO $.
      $( [29-May-2005] $) $( [30-Jun-2004] $)

    $( A consequence of relative atomicity.  Extensionality principle: two
       lattice elements are equal iff they majorize the same atoms. $)
    chrelat4i $p |- ( A = B <-> A. x e. Atoms ( x C_ A <-> x C_ B ) ) $=
      ( wss wa cv wi cat wral wceq wb chrelat3i anbi12i eqss dfbi2 ralbii
      r19.26 bitri 3bitr4i ) BCFZCBFZGAHZBFZUDCFZIZAJKZUFUEIZAJKZGZBCLUEUFMZAJK
      ZUBUHUCUJABCDENACBEDNOBCPUMUGUIGZAJKUKULUNAJUEUFQRUGUIAJSTUA $.
      $( [26-Jul-2005] $) $( [30-Jun-2004] $)
  $}

  $( The Hilbert lattice satisfies the exchange axiom.  Proposition 1(iii) of
     [Kalmbach] p. 140 and its converse.  Originally proved by Garrett Birkhoff
     in 1933. $)
  cvexch $p |- ( ( A e. CH /\ B e. CH ) ->
                ( ( A i^i B ) <o B <-> A <o ( A vH B ) ) ) $=
    ( cch wcel cin ccv wbr chj co wb chil cif wceq ineq1 breq1d id opreq1
    breq12d bibi12d ineq2 opreq2 breq2d helch elimel cvexchi dedth2h ) ACDZBCDZ
    ABEZBFGZAABHIZFGZJUGAKLZBEZBFGZUMUMBHIZFGZJUMUHBKLZEZURFGZUMUMURHIZFGZJABKK
    AUMMZUJUOULUQVCUIUNBFAUMBNOVCAUMUKUPFVCPAUMBHQRSBURMZUOUTUQVBVDUNUSBURFBURU
    MTVDPRVDUPVAUMFBURUMHUAUBSUMURAKCUCUDBKCUCUDUEUF $.
    $( [21-Jun-2004] $)

  $( The Hilbert lattice satisfies the covering property of Definition 7.4 of
     [MaedaMaeda] p. 31 and its converse. $)
  cvp $p |- ( ( A e. CH /\ B e. Atoms ) ->
            ( ( A i^i B ) = 0H <-> A <o ( A vH B ) ) ) $=
    ( cch wcel cat wa cin ccv wbr c0h wceq chj co wb chincl atelch sylan2
    atcveq0 sylancom cvexch bitr3d ) ACDZBEDZFABGZBHIZUDJKZAABLMHIZUBUCUDCDZUEU
    FNUBBCDZUHUCABOBPZQUDBRSUBUIUEUGNUCABTUJQUA $.
    $( [21-Jun-2004] $)

  $( The meet of a Hilbert lattice element and an incomparable atom is the zero
     subspace. $)
  atnssm0 $p |- ( ( A e. CH /\ B e. Atoms ) ->
             ( -. B C_ A <-> ( A i^i B ) = 0H ) ) $=
    ( cch wcel cat wa wss wn chj co ccv wbr cin c0h wceq chcv1 cvp bitr4d ) ACD
    BEDFBAGHAABIJKLABMNOABPABQR $.
    $( [30-Jun-2004] $)

  $( The meet of distinct atoms is the zero subspace. $)
  atnem0 $p |- ( ( A e. Atoms /\ B e. Atoms ) ->
             ( A =/= B <-> ( A i^i B ) = 0H ) ) $=
    ( cat wcel wa wss wn wne cin c0h wceq wb atsseq eqcom syl6bb ancoms
    necon3bbid cch atnssm0 atelch sylan bitr3d ) ACDZBCDZEZBAFZGZABHABIJKZUEUFA
    BUDUCUFABKZLUDUCEUFBAKUIBAMBANOPQARDUDUGUHLUCABSATUAUB $.
    $( [25-Sep-2009] $) $( [25-Jun-2004] $)

  $( The meet with an atom's superset is the atom. $)
  atssma $p |- ( ( A e. Atoms /\ B e. CH ) ->
            ( A C_ B <-> ( A i^i B ) e. Atoms ) ) $=
    ( cat wcel cch wa wss cin wi wceq df-ss biimpi eleq1d biimprcd adantr c0h
    wn wb atnssm0 ancoms biimpd con1d incom eleq1i wne atn0 df-ne sylib sylbi
    syl5 impbid ) ACDZBEDZFZABGZABHZCDZULUOUQIUMUOUQULUOUPACUOUPAJABKLMNOUNBAHZ
    PJZQZUOUQUNUOUSUNUOQZUSUMULVAUSRBASTUAUBUQURCDZUTUPURCABUCUDVBURPUEUTURUFUR
    PUGUHUIUJUK $.
    $( [12-Jun-2006] $)

  $( Two atoms covering the zero subspace are equal. $)
  atcv0eq $p |- ( ( A e. Atoms /\ B e. Atoms ) ->
               ( 0H <o ( A vH B ) <-> A = B ) ) $=
    ( cat wcel wa c0h chj co ccv wbr wceq wne wn cin atnem0 cch wb cvp atelch
    sylan atcv0 adantr biantrurd 3bitrd wi chjcl h0elch cvntr mp3an1 syldan
    syl2an sylbid necon4ad opreq1 chjidm syl sylan9eq eqcomd eleq1d ex ibd
    syl6com adantl impbid ) ACDZBCDZEZFABGHZIJZABKZVGVIABVGABLZFAIJZAVHIJZEZVIM
    ZVGVKABNFKZVMVNABOAPDZVFVPVMQVEABRASZTVGVLVMVEVLVFAUAUBUCUDVQBPDZVNVOUEZVEV
    FVQVSVHPDZVTABUFFPDVQWAVTUGFAVHUHUIUJVRBSZUKULUMVFVJVIUEVEVJVFVHCDZVIVJVFWC
    VJVFVFWCQVJVFEZBVHCWDVHBVJVFVHBBGHZBABBGUNVFVSWEBKWBBUOUPUQURUSUTVAVHUAVBVC
    VD $.
    $( [26-Jun-2004] $)

  $( Two atoms covering the zero subspace are equal. $)
  atcv1 $p |- ( ( ( A e. CH /\ B e. Atoms /\ C e. Atoms ) /\
                A <o ( B vH C ) ) -> ( A = 0H <-> B = C ) ) $=
    ( cch wcel cat w3a chj co ccv wbr wa c0h wceq wi breq1 atcv0eq sylan9bbr
    biimpd ex com23 3adant1 imp wb atcveq0 opreq1 atelch chjidm syl sylan9eq
    eqcomd eleq1d ibd impcom sylan2 exp32 com34 3adant2 impbid ) ADEZBFEZCFEZGZ
    ABCHIZJKZLAMNZBCNZVCVEVFVGOZVAVBVEVHOUTVAVBLZVFVEVGVIVFVEVGOVIVFLVEVGVFVEMV
    DJKVIVGAMVDJPBCQRSTUAUBUCVCVEVGVFOZUTVBVEVJOZVAUTVBVKUTVBVGVEVFUTVBVGVEVFOU
    TVBVGLZLVEVFUTVDFEZVEVFUDVLAVDUEVGVBVMVGVBVMVGVBVBVMUDVGVBLZCVDFVNVDCVGVBVD
    CCHIZCBCCHUFVBCDEVOCNCUGCUHUIUJUKULTUMUNUOSUPUQUCURUCUS $.
    $( [26-Jun-2004] $)

  $( The Hilbert lattice satisfies the atom exchange property.  Proposition
     1(i) of [Kalmbach] p. 140.  A version of this theorem related to vector
     analysis was originally proved by Hermann Grassmann in 1862.  Also
     Definition 3.4-3(b) in [MegillPavicic] p. 2345 (PDF p. 8) (use ~ atnem0 to
     obtain atom inequality). $)
  atexch $p |- ( ( A e. CH /\ B e. Atoms /\ C e. Atoms ) ->
            ( ( B C_ ( A vH C ) /\ ( A i^i B ) = 0H ) -> C C_ ( A vH B ) ) ) $=
    ( cch wcel cat w3a chj co wss cin c0h wceq wa chub2 ancoms atelch sylan2
    3adant2 adantr wpss wi ccv wbr cvp chjcl cvpss syldan sylbid 3adant3
    adantld chub1 a1d ancrd wb chlub syld3an3 sylibd id syl3an adantrd jcad
    imp 3simp1 3jca anim12d ancomsd psssstr syl6 chcv2 cvnbtwn2 sylsyld mpd
    sseqtr4d ex ) ADEZBFEZCFEZGZBACHIZJZABKLMZNZCABHIZJVSWCNZCVTWDVSCVTJZWCVPVR
    WFVQVPCDEZWFVRWGVPWFCAOPCQZRSTWEAWDUAZWDVTJZNZWDVTMZVSWCWKVSWCWIWJVSWBWIWAV
    PVQWBWIUBVRVPVQNWBAWDUCUDZWIABUEVPVQWDDEZWMWIUBVPBDEZWNVQABUFZBQZRAWDUGUHUI
    UJZUKVSWAWJWBVPWOWGWAWJUBVPVQVRVPWOWGGZWAAVTJZWANZWJWSWAWTWSWTWAVPWGWTWOACU
    LSUMUNVPWOVTDEZXAWJUOWGABVTUPVPWGXBWOACUFSZUQURVPUSZWQWHUTZVAVBVCVSWCWKWLUB
    ZVSVPXBWNGZWCAVTUCUDZXFVPWOWGXGVPVQVRWSVPXBWNVPWOWGVDXCVPWOWNWGWPUJVEXDWQWH
    UTVSWCAVTUAZXHVSWCWKXIVSWBWAWKVSWBWIWAWJWRXEVFVGAWDVTVHVIVPVRXIXHUOVQACVJSU
    RAVTWDVKVLVCVMVNVO $.
    $( [27-Jun-2004] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    atoml.1 $e |- A e. CH $.
    $( An assertion holding in atomic orthomodular lattices that is equivalent
       to the exchange axiom.  Proposition 3.2.17 of [PtakPulmannova] p. 66. $)
    atomli $p |- ( B e. Atoms ->
                ( ( A vH B ) i^i ( _|_ ` A ) ) e. ( Atoms u. { 0H } ) ) $=
      ( vx cat wcel chj co cort cfv cin c0h wceq wo csn cun wne wa cv wss wrex
      cch wi atelch chjcl mpan syl choccli chincl mpan2 hatomic ex 3syl imp wb
      pjoml3 pjococi opreq1i ineq1i incom eqtr3i syl5eq inss2 sstr syl2an
      ad2ant2lr chub1 adantr chlub mp3an1 sylan2 biimpd ancoms mpand inss1
      adantrr anim12i ad2antlr atexch pm3.22 adantl csh chsh chshii orthin jca
      sylc exp3a syl3c eqssd ineq1d eqtr3d eleq1d exp43 com24 imp31 ibd com23
      r19.23adv mpd necon1bd orrd elun fvex inex2 elsnc orbi2i bitri sylibr )
      BEFZABGHZAIJZKZEFZXMLMZNZXMELOZPFZXJXNXOXJXNXMLXJXMLQZXNXJXSRZDSZXMTZDEUA
      ZXNXJXSYCXJXKUBFZXMUBFZXSYCUCXJBUBFZYDBUDZAUBFZYFYDCABUEUFZUGYDXLUBFZYEAC
      UHZXKXLUIUJYEXSYCDXMUKULUMUNXTYBXNDEXTYBYAEFZXNXTYBYLXNUCXTYBRYLXNXJXSYBY
      LYLXNUOZUCXJYLYBXSYMXJYLYBXSYMXJYLRZYBXSRZRZYAXMEYPAYAGHZXLKZYAXMYLYBYRYA
      MZXJXSYAUBFZYAXLTZYSYLYBYTUUARZXLXLIJZYAGHZKZYAYRYTUUAUUEYAMZYJYTUUAUUFUC
      YKXLYAUPUFUNUUDXLKYRUUEUUDYQXLUUCAYAGACUQURUSUUDXLUTVAVBYAUDZYBXMXLTUUAXK
      XLVCYAXMXLVDUJZVEVFYPYQXKXLYPYQXKYNYBYQXKTZXSYNYAXKTZUUIYBYNUUJUUIYFYTUUJ
      UUIUCXJYLYFYTRAXKTZUUJUUIYFUUKYTYHYFUUKCABVGUFVHYTYFUUKUUJRZUUIUCYTYFRUUL
      UUIYTYDUULUUIUOZYFYHYTYDUUMCAYAXKVIVJYIVKVLVMVNYGUUGVEUNYBXMXKTUUJXKXLVOY
      AXMXKVDUJZVKVPYPYFYQUBFZRZAYQTZBYQTZXKYQTZYNUUPYOXJYFYLUUOYGYLYTUUOUUGYHY
      TUUOCAYAUEUFUGVQVHYLUUQXJYOYLYTUUQUUGYHYTUUQCAYAVGUFUGVRYLXJRZUUJAYAKZLMZ
      RZUURYPYHYLXJUVCUURUCCAYABVSVJYNUUTYOXJYLVTVHYLYBUVCXJXSYLYBRUUJUVBYBUUJY
      LUUNWAYTUUAUVBYLYBUUBYAAKZLUVAYTUUAUVDLMZYTYAWBFZUUAUVEUCZYAWCUVFAWBFUVGA
      CWDYAAWEUJUGUNAYAUTVBUUGUUHVEWFVFWGUUPUUQUURUUSUUPUUQUURRZUUSYHYFUUOUVHUU
      SUOCABYQVIVJVLWHWIWJWKWLWMWNWOWPWQULWRWSWTULXAXBXRXNXMXQFZNXPXMEXQXCUVIXO
      XNXMLXLXKAIXDXEXFXGXHXI $.
      $( [9-May-2005] $) $( [24-Jun-2004] $)

    $( An assertion holding in atomic orthomodular lattices that is equivalent
       to the exchange axiom.  Proposition P8(ii) of [BeltramettiCassinelli1]
       p. 400. $)
    atoml2i $p |- ( ( B e. Atoms /\ -. B C_ A ) ->
                ( ( A vH B ) i^i ( _|_ ` A ) ) e. Atoms ) $=
      ( cat wcel wss wn chj co cort cfv cin c0h wceq cch atelch pjoml5 mpan
      syl incom eqeq1i biimpi opreq2d chj0i syl6eq sylan9req ex wb chlejb2
      mpan2 sylibrd con3d csn cun wo atomli elun h0elch elisseti elsnc2 orbi2i
      orcom 3bitri sylib ord syld imp ) BDEZBAFZGZABHIZAJKZLZDEZVHVJVMMNZGVNVHV
      OVIVHVOVKANZVIVHVOVPVHVOVKAVLVKLZHIZAVHBOEZVRVKNZBPZAOEZVSVTCABQRSVOVRAMH
      IAVOVQMAHVOVQMNVMVQMVKVLTUAUBUCACUDUEUFUGVHVSVIVPUHZWAVSWBWCCBAUIUJSUKULV
      HVOVNVHVMDMUMZUNEZVOVNUOZABCUPWEVNVMWDEZUOVNVOUOWFVMDWDUQWGVOVNVMMMOURUSU
      TVAVNVOVBVCVDVEVFVG $.
      $( [12-Jun-2006] $)

    $( An ordering law for a Hilbert lattice atom and a commuting subspace. $)
    atordi $p |- ( ( B e. Atoms /\ A C_H B ) ->
                ( B C_ A \/ B C_ ( _|_ ` A ) ) ) $=
      ( cat wcel ccm wbr wa wss cort cfv wn cin chj co wceq cch c0h choccli
      chincl mpan chj0 syl incom syl6eq h0elch chjcom mpan2 eqtr3d chocini
      eqtri opreq1i syl6eqr adantr w3a cmidi cmcm2ii fh2 mpanr1 mp3anl2 mpanl1
      eqtr4d atelch sylan atoml2i adantlr eqeltrd wb atssma ad2antrr mpbird ex
      orrd ) BDEZABFGZHZBAIZBAJKZIZVPVQLZVSVPVTHZVSBVRMZDEZWAWBABNOZVRMZDVPWBWE
      PVTVPWBVRWDMZWEBQEZVOWBWFPVNWGVOHWBVRAMZVRBMZNOZWFWGWBWJPVOWGWBRWINOZWJWG
      WIRNOZWBWKWGWLWIWBWGWIQEZWLWIPVRQEZWGWMACSZVRBTUAZWIUBUCVRBUDUEWGWMWLWKPZ
      WPWMRQEWQUFWIRUGUHUCUIWHRWINWHAVRMRVRAUDACUJUKULUMUNWNWGVOWFWJPZWOWNAQEZW
      GVOWRCWNWSWGUOAVRFGVOWRAACCACUPUQVRABURUSUTVAVBBVCVDVRWDUDUEUNVNVTWEDEVOA
      BCVEVFVGVNVSWCVHZVOVTVNWNWTWOBVRVIUHVJVKVLVM $.
      $( [12-Jun-2006] $)

    $( Lemma for ~ atcvati . $)
    atcvatlem $p |- ( ( ( B e. Atoms /\ C e. Atoms ) /\
        ( A =/= 0H /\ A C. ( B vH C ) ) ) -> ( -. B C_ A -> A e. Atoms ) ) $=
      ( vx cat wcel wa c0h wne chj co wpss wss wn wi cv wrex ccv wbr wceq cin
      atnem0 sseq1 biimpcd necon3bd imp adantrl syl5bi cch wb cvp chjcom
      atelch sylan2 breq2d bitrd sylan sylibd ancoms adantlr w3a 3simp1 3simp3
      chjcl 3adant3 3jca syl3an chlub syl adantr chub1 syl2an sstr pssss
      adantl incom syl5eq adantrr atexch syl3an1 mp2and mpbi2and 3adant2
      anim12i syld3an3 mpbid syl3anl eqssd anassrs psseq2d ex ibd exp32 3expa
      an1rs com34 imp45 pm3.27 jca cvnbtwn3 mp3an3 exp4a com23 imp4a eleq1d
      biimprcd exp4c pm2.43b exp4d r19.23adva hatomici syl5 imp32 ) BFGZCFGZHZA
      IJZABCKLZMZBANZOZAFGZPZXQEQZANZEFRXTYDPZXRXQYFYGEFXQYEFGZHZYFXTYBYCXQYHYF
      XTYBHZHZYCPZXQYHYLYHXQYHYKYCYIYKHZYCYHYMAYEFYMYEBYEKLZSTZAYNMZAYEUAZYIYKY
      OXOYHYKYOPZXPYHXOYRYHXOHZYKYEBUBZIUAZYOYSYEBJZUUAYKYEBUCZYFYBUUBXTYFYBUUB
      YFYAYEBYEBUAYFYAYEBAUDUEUFUGZUHUIYEUJGZXOUUAYOUKYHUUEXOHZUUAYEYEBKLZSTYOY
      EBULUUFUUGYNYESUUEBUJGZUUGYNUAXOYEBUMBUNZUOUPUQYEUNZURUSUTVAUGYIYFXTYBYPY
      IYFYBXTYPXOYHXPYFYBXTYPPZPPZXOYHXPUULXOYHXPVBZYFYBUUKUUMYFYBHZHZXTYPUUOXT
      XTYPUKUUOXTHXSYNAUUMUUNXTXSYNUAUUMUUNXTHZHZXSYNUUQBYNNZCYNNZXSYNNZUUMUURU
      USHUUTUKZUUPUUMUUHCUJGZYNUJGZVBZUVAUUHUUEUVBUVDXOYHXPUUHUUEUVBVBZUUHUVBUV
      CUUHUUEUVBVCUUHUUEUVBVDUUHUUEUVCUVBBYEVEZVFVGUUIUUJCUNZVHBCYNVIVJVKUUMUUR
      UUPXOYHUURXPUUHUUEUURXOYHBYEVLUUIUUJVMVFVKUUQYEXSNZBYEUBZIUAZUUSUUPUVHUUM
      YFXTUVHYBYFAXSNUVHXTYEAXSVNAXSVOUOVAZVPUUMUUNUVJXTUUOYTIUVIUUMUUNUUAXOYHU
      UNUUAPZXPYHXOUVLYSUUBUUAUUNUUCUUDUIUTVFUGBYEVQVRVSUUMUVHUVJHUUSPZUUPUUHYH
      XPUVMXOBYECVTUUIWAVKWBWCUUHUUEUVBUUPYNXSNZXOYHXPUVEUUPHBXSNZUVHHZUVNUVEUV
      OUUPUVHUUHUVBUVOUUEBCVLWDUVKWEUVEUVPUVNUKZUUPUUHUUEXSUJGZUVQUVBBYEXSVIUUH
      UVBUVRUUEBCVEWDWFVKWGUUIUUJUVGWHWIWJWKWLWMWNWOWPWQWRYIYFYOYPHYQPZYJYIYFUV
      SXOYHYFUVSPZXPXOYHHUUEUVCHZUVTUUHUUEUWAXOYHUUHUUEHUUEUVCUUHUUEWSUVFWTUUIU
      UJVMUWAYFYOYPYQUWAYOYFYPYQPUWAYOYFYPYQUUEUVCAUJGYOYFYPHYQPPDYEYNAXAXBXCXD
      XEVJVAUGVSWBXFXGXHXIUGXJXKEADXLXMXN $.
      $( [23-Mar-2007] $) $( [27-Jun-2004] $)

    $( A nonzero Hilbert lattice element less than the join of two atoms is an
       atom. $)
    atcvati $p |- ( ( B e. Atoms /\ C e. Atoms ) ->
                 ( ( A =/= 0H /\ A C. ( B vH C ) ) -> A e. Atoms ) ) $=
      ( cat wcel wa c0h wne chj co wpss wss wn wo cch wi w3a wb chlub 3comr
      ssnpss syl6bi con2d ianor syl6ib mp3an1 atelch syl2an imp adantrl
      atcvatlem wceq chjcom psseq2d anbi2d ex sylbird ancoms jaod mpd ) BEFZCEF
      ZGZAHIZABCJKZLZGZAEFZVDVHGZBAMZNZCAMZNZOZVIVDVGVOVEVDVGVOBPFZCPFZVGVOQZVB
      VCAPFZVPVQVRDVSVPVQRZVGVKVMGZNVOVTWAVGVTWAVFAMZVGNVPVQVSWAWBSBCATUAVFAUBU
      CUDVKVMUEUFUGBUHZCUHZUIUJUKVJVLVIVNABCDULVDVHVNVIQZVCVBVHWEQVCVBGZVHVEACB
      JKZLZGZWEWFWHVGVEWFWGVFAVQVPWGVFUMVCVBCBUNWDWCUIUOUPWFWIWEACBDULUQURUSUJU
      TVAUQ $.
      $( [25-Mar-2007] $) $( [28-Jun-2004] $)

    $( A Hilbert lattice element covered by the join of two distinct atoms is
       an atom. $)
    atcvat2i $p |- ( ( B e. Atoms /\ C e. Atoms ) ->
                 ( ( -. B = C /\ A <o ( B vH C ) ) -> A e. Atoms ) ) $=
      ( cat wcel wa wceq wn chj co ccv wbr wi c0h wne cch wb atcv1 mp3anl1
      necon3abid wpss chjcl atelch syl2an cvpss mpan syl atcvati exp3a com23
      syld imp sylbird ex imp3a ) BEFZCEFZGZBCHZIZABCJKZLMZAEFZUSVCVAVDUSVCVAVD
      NUSVCGZVAAOPZVDVEUTAOAQFZUQURVCAOHUTRDABCSTUAUSVCVFVDNZUSVCAVBUBZVHUSVBQF
      ZVCVINZBQFCQFVJUQURBCUCBUDCUDUEVGVJVKDAVBUFUGUHUSVFVIVDUSVFVIVDABCDUIUJUK
      ULUMUNUOUKUP $.
      $( [26-Jun-2004] $)
  $}

  $( An ordering law for a Hilbert lattice atom and a commuting subspace. $)
  atord $p |- ( ( A e. CH /\ B e. Atoms /\ A C_H B ) ->
              ( B C_ A \/ B C_ ( _|_ ` A ) ) ) $=
    ( cch wcel cat ccm wbr wss cort cfv wo wa wi c0h cif wceq breq1 anbi2d
    sseq2 fveq2 sseq2d orbi12d imbi12d h0elch elimel atordi dedth 3impib ) ACDZ
    BEDZABFGZBAHZBAIJZHZKZUIUJUKLZUOMUJUIANOZBFGZLZBUQHZBUQIJZHZKZMANAUQPZUPUSU
    OVCVDUKURUJAUQBFQRVDULUTUNVBAUQBSVDUMVABAUQITUAUBUCUQBANCUDUEUFUGUH $.
    $( [12-Jun-2006] $)

  $( A Hilbert lattice element covered by the join of two distinct atoms is an
     atom. $)
  atcvat2 $p |- ( ( A e. CH /\ B e. Atoms /\ C e. Atoms ) ->
               ( ( -. B = C /\ A <o ( B vH C ) ) -> A e. Atoms ) ) $=
    ( cch wcel cat wceq wn chj co ccv wbr wa wi c0h cif breq1 anbi2d eleq1
    imbi12d imbi2d h0elch elimel atcvat2i dedth 3impib ) ADEZBFEZCFEZBCGHZABCIJ
    ZKLZMZAFEZNZUGUHUIMZUONUPUJUGAOPZUKKLZMZUQFEZNZNAOAUQGZUOVAUPVBUMUSUNUTVBUL
    URUJAUQUKKQRAUQFSTUAUQBCAODUBUCUDUEUF $.
    $( [29-Jun-2004] $)

