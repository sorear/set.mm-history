$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Quantum Logic Explorer axioms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    qlax1.1 $e |- A e. CH $.
    $( One of the equations showing ` CH ` is an ortholattice.  (This
       corresponds to axiom "ax-1" in the Quantum Logic Explorer.) $)
    qlax1 $p |- A = ( _|_ ` ( _|_ ` A ) ) $=
      ( cort cfv ococ eqcomi ) ACDCDAABEF $.
      $( [15-Aug-2004] $) $( [4-Aug-2004] $)
  $}

  ${
    qlax.1 $e |- A e. CH $.
    qlax.2 $e |- B e. CH $.
    $( One of the equations showing ` CH ` is an ortholattice.  (This
       corresponds to axiom "ax-2" in the Quantum Logic Explorer.) $)
    qlax2 $p |- ( A vH B ) = ( B vH A ) $=
      ( chjcom ) ABCDE $.
      $( [15-Aug-2004] $) $( [4-Aug-2004] $)

    ${
      qlax.3 $e |- C e. CH $.
      $( One of the equations showing ` CH ` is an ortholattice.  (This
         corresponds to axiom "ax-3" in the Quantum Logic Explorer.) $)
      qlax3 $p |- ( ( A vH B ) vH C ) = ( A vH ( B vH C ) ) $=
        ( chjass ) ABCDEFG $.
        $( [15-Aug-2004] $) $( [4-Aug-2004] $)
    $}

    $( One of the equations showing ` CH ` is an ortholattice.  (This
       corresponds to axiom "ax-4" in the Quantum Logic Explorer.) $)
    qlax4 $p |- ( A vH ( B vH ( _|_ ` B ) ) ) = ( B vH ( _|_ ` B ) ) $=
      ( chil chj co cort cfv chj1 chjo opreq2i 3eqtr4 ) AEFGEABBHIFGZFGNACJNEAF
      BDKZLOM $.
      $( [15-Aug-2004] $) $( [4-Aug-2004] $)

    $( One of the equations showing ` CH ` is an ortholattice.  (This
       corresponds to axiom "ax-5" in the Quantum Logic Explorer.) $)
    qlax5 $p |- ( A vH ( _|_ ` ( ( _|_ ` A ) vH B ) ) ) = A $=
      ( cort cfv chj co cin chdmj2 opreq2i choccl chabs1 eqtr ) AAEFBGHEFZGHAAB
      EFZIZGHAOQAGABCDJKAPCBDLMN $.
      $( [15-Aug-2004] $) $( [4-Aug-2004] $)
  $}

  ${
    qlaxr1.1 $e |- A e. CH $.
    qlaxr1.2 $e |- B e. CH $.
    qlaxr1.3 $e |- A = B $.
    $( One of the conditions showing ` CH ` is an ortholattice.  (This
       corresponds to axiom "ax-r1" in the Quantum Logic Explorer.) $)
    qlaxr1 $p |- B = A $=
      ( eqcomi ) ABEF $.
      $( [15-Aug-2004] $) $( [4-Aug-2004] $)
  $}

  ${
    qlaxr2.1 $e |- A e. CH $.
    qlaxr2.2 $e |- B e. CH $.
    qlaxr2.3 $e |- C e. CH $.
    qlaxr2.4 $e |- A = B $.
    qlaxr2.5 $e |- B = C $.
    $( One of the conditions showing ` CH ` is an ortholattice.  (This
       corresponds to axiom "ax-r2" in the Quantum Logic Explorer.) $)
    qlaxr2 $p |- A = C $=
      ( eqtr ) ABCGHI $.
      $( [15-Aug-2004] $) $( [4-Aug-2004] $)
  $}

  ${
    qlaxr4.1 $e |- A e. CH $.
    qlaxr4.2 $e |- B e. CH $.
    qlaxr4.3 $e |- A = B $.
    $( One of the conditions showing ` CH ` is an ortholattice.  (This
       corresponds to axiom "ax-r4" in the Quantum Logic Explorer.) $)
    qlaxr4 $p |- ( _|_ ` A ) = ( _|_ ` B ) $=
      ( cort fveq2i ) ABFEG $.
      $( [15-Aug-2004] $) $( [4-Aug-2004] $)
  $}

  ${
    qlaxr5.1 $e |- A e. CH $.
    qlaxr5.2 $e |- B e. CH $.
    qlaxr5.3 $e |- C e. CH $.
    qlaxr5.4 $e |- A = B $.
    $( One of the conditions showing ` CH ` is an ortholattice.  (This
       corresponds to axiom "ax-r5" in the Quantum Logic Explorer.) $)
    qlaxr5 $p |- ( A vH C ) = ( B vH C ) $=
      ( chj opreq1i ) ABCHGI $.
      $( [15-Aug-2004] $) $( [4-Aug-2004] $)
  $}

  ${
    qlaxr3.1 $e |- A e. CH $.
    qlaxr3.2 $e |- B e. CH $.
    qlaxr3.3 $e |- C e. CH $.
    qlaxr3.4 $e |- ( C vH ( _|_ ` C ) ) =
        ( ( _|_ ` ( ( _|_ ` A ) vH ( _|_ ` B ) ) ) vH ( _|_ ` ( A vH B ) ) ) $.
    $( A variation of the orthomodular law, showing ` CH ` is an orthomodular
       lattice.  (This corresponds to axiom "ax-r3" in the Quantum Logic
       Explorer.) $)
    qlaxr3 $p |- A = B $=
      ( chj co chjcl chshi chub1 cort cfv cin c0h wceq wa incom choccl cmj1
      cmcmi cmcm2i cmj2 fh1 chil chjo eqtr3 choc0 chdmm1 3eqtr4 chincl h0elch
      chcon3 mpbir 3eqtr3 chj00 pm3.26i omlsi chub2 pm3.27i eqtr4 ) AABHIZBAVCD
      VCABDEJZKZABDELVCAMNZOZPQZVCBMNZOZPQZVHVKRVGVJHIZPQVCVFVIHIZOVMVCOZVLPVCV
      MSVCVFVIVDADTZBETZVCAVDDAVCDVDABDEUAUBUCVCBVDEBVCEVDABDEUDUBUCUEVNPQPMNZV
      NMNZQUFVMMNVCMNHIZVQVRCCMNHIUFVSCFUGGUHUIVMVCVFVIVOVPJZVDUJUKVNPVMVCVTVDU
      LUMUNUOUPVGVJVCVFVDVOULVCVIVDVPULUQUOZURUSBVCEVEBAEDUTVHVKWAVAUSVB $.
      $( [15-Aug-2004] $) $( [7-Aug-2004] $)
  $}

