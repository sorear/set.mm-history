$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Bunjakovaskij-Cauchy-Schwarz inequality
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    bcs.1 $e |- A e. H~ $.
    bcs.2 $e |- B e. H~ $.
    $( Bunjakovaskij-Cauchy-Schwarz inequality.  Remark 3.4 of [Beran]
       p. 98. $)
    bcsiALT $p |- ( abs ` ( A .ih B ) )
          <_ ( ( normh ` A ) x. ( normh ` B ) ) $=
      ( csp co cc0 wceq cabs cfv cno cmul cle wbr fveq2 abs0 chil wcel normge0
      ax-mp normcli mulge0i mp2an eqbrtri syl6eqbr wn c2 csqr wne df-ne cdiv
      ccj caddc hicli abslem2 his1i opreq2i syl5req cc c1 wa abs00i necon3bii
      abscli recni divclzi divreczi fveq2d recclzi jctil absmul syl cr absid
      rerecclzi clt ltle 0re absgt0i bitri recgt0i sylbi sylc sylanc opreq2d
      eqtrd recidzi 3eqtrd jca sylbir normlem7tALT eqbrtrd mulcomi normval
      opreq12i eqtri breq2i wb 2pos hiidge0 hiidrcl sqrcli remulcli 2re
      lemul2i sylibr pm2.61i ) ABEFZGHZXHIJZAKJZBKJZLFZMNZXIXJGIJZXMMXHGIOXOGXM
      MPGXKMNZGXLMNZGXMMNAQRZXPCASTBQRZXQDBSTXKXLACUAZBDUAZUBUCUDUEXIUFZUGXJLFZ
      UGBBEFZUHJZAAEFZUHJZLFZLFZMNZXNYBXHGUIZYJXHGUJYKYCXHXJUKFZULJXHLFZYLBAEFZ
      LFZUMFZYIMYKYMYLXHULJZLFZUMFYCYPXHABCDUNZUOYOYRYMUMYNYQYLLBADCUPUQUQURYKY
      LUSRZYLIJZUTHZVAZYPYIMNYKXJGUIZUUCXJGXHGXHYSVBVCZUUDYTUUBXHXJYSXJXHYSVDZV
      EZVFUUDUUAXHUTXJUKFZLFZIJZXJUUHLFZUTUUDYLUUIIXHXJYSUUGVGVHUUDUUJXJUUHIJZL
      FZUUKUUDXHUSRZUUHUSRZVAUUJUUMHUUDUUOUUNXJUUGVIYSVJXHUUHVKVLUUDUULUUHXJLUU
      HVMRZGUUHMNZUULUUHHUUDUUHVNXJUUFVOZGVMRZUUPVAGUUHVPNZUUQUUDGUUHVQUUDUUPUU
      SUURVRVJUUDGXJVPNZUUTUUDYKUVAUUEXHYSVSVTXJUUFWAWBWCWDWEWFXJUUGWGWHWIWJABY
      LCDWKVLWLWJXNXJYHMNZYJXMYHXJMXMXLXKLFYHXKXLXKXTVEXLYAVEWMXLYEXKYGLXSXLYEH
      DBWNTXRXKYGHCAWNTWOWPWQGUGVPNUVBYJWRWSXJYHUGUUFYEYGGYDMNZYEVMRXSUVCDBWTTY
      DXSYDVMRDBXATXBTGYFMNZYGVMRXRUVDCAWTTYFXRYFVMRCAXATXBTXCXDXETVTXFXG $.
      $( [11-Oct-1999] $)

    $( Bunjakovaskij-Cauchy-Schwarz inequality.  Remark 3.4 of [Beran]
       p. 98.  (Proved from ZFC version.) $)
    bcsiHIL $p |- ( abs ` ( A .ih B ) )
         <_ ( ( normh ` A ) x. ( normh ` B ) ) $=
      ( csp cva csm cop cno chil eqid hhba hhnm hhip hhph siii ) ABEFGHIHZIJQQK
      ZLQRMQRNQROCDP $.
      $( [19-Mar-2008] $) $( [24-Nov-2007] $)
  $}

  $( Bunjakovaskij-Cauchy-Schwarz inequality.  Remark 3.4 of [Beran] p. 98. $)
  bcs $p |- ( ( A e. H~ /\ B e. H~ ) ->
             ( abs ` ( A .ih B ) ) <_ ( ( normh ` A ) x. ( normh ` B ) ) ) $=
    ( chil wcel csp co cabs cfv cno cmul cle wbr c0v cif wceq opreq1 fveq2d
    fveq2 opreq1d breq12d opreq2 opreq2d ax-hv0cl elimel bcsiHIL dedth2h ) ACDZ
    BCDZABEFZGHZAIHZBIHZJFZKLUGAMNZBEFZGHZUNIHZULJFZKLUNUHBMNZEFZGHZUQUSIHZJFZK
    LABMMAUNOZUJUPUMURKVDUIUOGAUNBEPQVDUKUQULJAUNIRSTBUSOZUPVAURVCKVEUOUTGBUSUN
    EUAQVEULVBUQJBUSIRUBTUNUSAMCUCUDBMCUCUDUEUF $.
    $( [19-Feb-2006] $) $( [16-Feb-2006] $)

  $( Corollary of the Bunjakovaskij-Cauchy-Schwarz inequality ~ bcsiHIL . $)
  bcs2 $p |- ( ( A e. H~ /\ B e. H~ /\ ( normh ` A ) <_ 1 ) ->
             ( abs ` ( A .ih B ) ) <_ ( normh ` B ) ) $=
    ( chil wcel cno cfv c1 cle wbr w3a csp co cabs cmul cr wa cc hicl abscl
    syl 3adant3 remulcl normcl syl2an 3ad2ant2 bcs cc0 1re lemul1aOLD mp3anl2
    3ad2ant1 normge0 3simp3 syl2anc wceq recnd mulid2 breqtrd letrd ) ACDZBCDZA
    EFZGHIZJZABKLZMFZVBBEFZNLZVGUTVAVFODZVCUTVAPVEQDVIABRVESTUAUTVAVHODZVCVBODZ
    VGODZVJUTVAVBVGUBAUCZBUCZUDUAVAUTVLVCVNUEZUTVAVFVHHIVCABUFUAVDVHGVGNLZVGHVK
    VLUGVGHIZVCVHVPHIZVDVKGODVLVQVCPVRUHVBGVGUIUJUTVAVKVCVMUKVOVAUTVQVCBULUEUTV
    AVCUMUNVAUTVPVGUOZVCVAVGQDVSVAVGVNUPVGUQTUEURUS $.
    $( [18-Jun-2006] $) $( [24-May-2006] $)

  $( Corollary of the Bunjakovaskij-Cauchy-Schwarz inequality ~ bcsiHIL . $)
  bcs3 $p |- ( ( A e. H~ /\ B e. H~ /\ ( normh ` B ) <_ 1 ) ->
             ( abs ` ( A .ih B ) ) <_ ( normh ` A ) ) $=
    ( chil wcel cno cfv c1 cle wbr w3a csp co cabs wceq abshicom 3adant3 bcs2
    3com12 eqbrtrd ) ACDZBCDZBEFGHIZJABKLMFZBAKLMFZAEFZHTUAUCUDNUBABOPUATUBUDUE
    HIBAQRS $.
    $( [23-Jun-2006] $) $( [26-May-2006] $)

