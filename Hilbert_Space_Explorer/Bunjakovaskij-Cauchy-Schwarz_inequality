$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Bunjakovaskij-Cauchy-Schwarz inequality
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    bcs.1 $e |- A e. H~ $.
    bcs.2 $e |- B e. H~ $.
    $( Bunjakovaskij-Cauchy-Schwarz inequality.  Remark 3.4 of [Beran]
       p. 98. $)
    bcsALT $p |- ( abs ` ( A .ih B ) ) <_ ( ( normh ` A ) x. ( normh ` B ) ) $=
      ( csp co cc0 wceq cabs cfv cno cmul cle wbr fveq2 abs0 chil wcel
      normge0t ax-mp normcl mulge0 mp2an eqbrtr syl6eqbr wn c2 csqr wne df-ne
      cdiv ccj caddc hicl abslem2 his1 opreq2i syl5req cc c1 wa abs00
      necon3bii abscl recn divclz divrecz fveq2d recclz jctil absmult syl cr
      absidt rerecclz clt ltlet 0re absgt0 bitr recgt0 sylbi sylc sylanc
      opreq2d eqtrd recidz 3eqtrd jca sylbir normlem7tALT eqbrtrd mulcom normvalt
      opreq12i eqtr breq2i wb 2pos hiidge0t hiidrclt sqrcl remulcl 2re lemul2
      sylibr pm2.61i ) ABEFZGHZXHIJZAKJZBKJZLFZMNZXIXJGIJZXMMXHGIOXOGXMMPGXKMNZ
      GXLMNZGXMMNAQRZXPCASTBQRZXQDBSTXKXLACUAZBDUAZUBUCUDUEXIUFZUGXJLFZUGBBEFZU
      HJZAAEFZUHJZLFZLFZMNZXNYBXHGUIZYJXHGUJYKYCXHXJUKFZULJXHLFZYLBAEFZLFZUMFZY
      IMYKYMYLXHULJZLFZUMFYCYPXHABCDUNZUOYOYRYMUMYNYQYLLBADCUPUQUQURYKYLUSRZYLI
      JZUTHZVAZYPYIMNYKXJGUIZUUCXJGXHGXHYSVBVCZUUDYTUUBXHXJYSXJXHYSVDZVEZVFUUDU
      UAXHUTXJUKFZLFZIJZXJUUHLFZUTUUDYLUUIIXHXJYSUUGVGVHUUDUUJXJUUHIJZLFZUUKUUD
      XHUSRZUUHUSRZVAUUJUUMHUUDUUOUUNXJUUGVIYSVJXHUUHVKVLUUDUULUUHXJLUUHVMRZGUU
      HMNZUULUUHHUUDUUHVNXJUUFVOZGVMRZUUPVAGUUHVPNZUUQUUDGUUHVQUUDUUPUUSUURVRVJ
      UUDGXJVPNZUUTUUDYKUVAUUEXHYSVSVTXJUUFWAWBWCWDWEWFXJUUGWGWHWIWJABYLCDWKVLW
      LWJXNXJYHMNZYJXMYHXJMXMXLXKLFYHXKXLXKXTVEXLYAVEWMXLYEXKYGLXSXLYEHDBWNTXRX
      KYGHCAWNTWOWPWQGUGVPNUVBYJWRWSXJYHUGUUFYEYGGYDMNZYEVMRXSUVCDBWTTYDXSYDVMR
      DBXATXBTGYFMNZYGVMRXRUVDCAWTTYFXRYFVMRCAXATXBTXCXDXETVTXFXG $.
      $( [11-Oct-1999] $)

    $( Bunjakovaskij-Cauchy-Schwarz inequality.  Remark 3.4 of [Beran]
       p. 98.  (Proved from ZFC version.) $)
    bcsHIL $p |- ( abs ` ( A .ih B ) ) <_ ( ( normh ` A ) x. ( normh ` B ) ) $=
      ( csp cva csm cop cno chil eqid hhba hhnm hhip hhph siii ) ABEFGHIHZIJQQK
      ZLQRMQRNQROCDP $.
      $( [19-Mar-2008] $) $( [24-Nov-2007] $)
  $}

  $( Bunjakovaskij-Cauchy-Schwarz inequality.  Remark 3.4 of [Beran] p. 98. $)
  bcst $p |- ( ( A e. H~ /\ B e. H~ ) ->
             ( abs ` ( A .ih B ) ) <_ ( ( normh ` A ) x. ( normh ` B ) ) ) $=
    ( chil wcel csp co cabs cfv cno cmul cle wbr c0v cif wceq opreq1 fveq2d
    fveq2 opreq1d breq12d opreq2 opreq2d ax-hv0cl elimel bcsHIL dedth2h ) ACDZBCDZ
    ABEFZGHZAIHZBIHZJFZKLUGAMNZBEFZGHZUNIHZULJFZKLUNUHBMNZEFZGHZUQUSIHZJFZKLABM
    MAUNOZUJUPUMURKVDUIUOGAUNBEPQVDUKUQULJAUNIRSTBUSOZUPVAURVCKVEUOUTGBUSUNEUAQ
    VEULVBUQJBUSIRUBTUNUSAMCUCUDBMCUCUDUEUF $.
    $( [19-Feb-2006] $) $( [16-Feb-2006] $)

  $( Corollary of the Bunjakovaskij-Cauchy-Schwarz inequality ~ bcsHIL . $)
  bcs2t $p |- ( ( A e. H~ /\ B e. H~ /\ ( normh ` A ) <_ 1 ) ->
             ( abs ` ( A .ih B ) ) <_ ( normh ` B ) ) $=
    ( chil wcel cno cfv c1 cle wbr w3a csp co cabs cmul cr wa cc hiclt absclt
    syl 3adant3 axmulrcl normclt syl2an 3ad2ant2 bcst cc0 1re lemul1itOLD
    mp3anl2 3ad2ant1 normge0t 3simp3 syl2anc wceq recnd mulid2t breqtrd letrd
    ) ACDZBCDZAEFZGHIZJZABKLZMFZVBBEFZNLZVGUTVAVFODZVCUTVAPVEQDVIABRVESTUAUTVAV
    HODZVCVBODZVGODZVJUTVAVBVGUBAUCZBUCZUDUAVAUTVLVCVNUEZUTVAVFVHHIVCABUFUAVDVH
    GVGNLZVGHVKVLUGVGHIZVCVHVPHIZVDVKGODVLVQVCPVRUHVBGVGUIUJUTVAVKVCVMUKVOVAUTV
    QVCBULUEUTVAVCUMUNVAUTVPVGUOZVCVAVGQDVSVAVGVNUPVGUQTUEURUS $.
    $( [18-Jun-2006] $) $( [24-May-2006] $)

  $( Corollary of the Bunjakovaskij-Cauchy-Schwarz inequality ~ bcsHIL . $)
  bcs3t $p |- ( ( A e. H~ /\ B e. H~ /\ ( normh ` B ) <_ 1 ) ->
             ( abs ` ( A .ih B ) ) <_ ( normh ` A ) ) $=
    ( chil wcel cno cfv c1 cle wbr w3a csp co cabs wceq abshicomt 3adant3
    bcs2t 3com12 eqbrtrd ) ACDZBCDZBEFGHIZJABKLMFZBAKLMFZAEFZHTUAUCUDNUBABOPUAT
    UBUDUEHIBAQRS $.
    $( [23-Jun-2006] $) $( [26-May-2006] $)

