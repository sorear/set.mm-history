$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Fields and Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Com2 $.

  $( Extend class notation with a class that adds commutativity to various
     flavors of rings. $)
  ccm2 $a class Com2 $.

  ${
    $d g h a b $.
    $( A device to add commutativity to various sorts of rings.  I use
       ` ran g ` because I suppose ` g ` has a neutral element and therefore is
       onto. $)
    df-com2 $a |- Com2 = { <. g , h >. | A. a e. ran g A. b e. ran g
     ( a h b ) = ( b h a ) } $.
  $}

  ${
    $d G a b x y $.  $d H a b x y $.
    $( A device to add commutativity to various sorts of rings.  (Contributed
       by FL, 6-Sep-2009.) $)
    iscom2 $p |- ( ( G e. A /\ H e. B ) -> ( <. G , H >. e. Com2 <->
     A. a e. ran G A. b e. ran G ( a H b ) = ( b H a ) ) ) $=
      ( vy vx wcel wa cop ccm2 cv co wceq crn wral copab df-com2 oveq raleqbidv
      a1i eleq2d rneq raleqdv eqeq12d 2ralbidv opelopabg bitrd ) CAIDBIJZCDKZLI
      UKEMZFMZGMZNZUMULUNNZOZFHMZPZQZEUSQZHGRZIULUMDNZUMULDNZOZFCPZQEVFQZUJLVBU
      KLVBOUJHGEFSUBUCVAUQFVFQZEVFQVGHGCDABURCOZUTVHEUSVFURCUDZVIUQFUSVFVJUEUAU
      NDOZUQVEEFVFVFVKUOVCUPVDULUMUNDTUMULUNDTUFUGUHUI $.
      $( [6-Sep-2009] $)
  $}

  $c Fld $.

  $( Extend class notation with the class of all fields. $)
  cfld $a class Fld $.

  $( Add alternate definition. Add Kennington's one (=/= the zero ring),
       and Mizar's one: ` ( Id `` G ) =/= ( Id `` H ) ` $)
  $( Definition of a field.  A field is a commutative division ring. $)
  df-fld $a |- Fld = ( DivRingOps i^i Com2 ) $.

  $( A field is a division ring.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
  flddivrng $p |- ( K e. Fld -> K e. DivRingOps ) $=
    ( cfld cdrng ccm2 cin df-fld inss1 eqsstri sseli ) BCABCDECFCDGHI $.
    $( [15-Dec-2013] $) $( [10-Jun-2010] $)

  ${
    rngn0.1 $e |- G = ( 1st ` R ) $.
    rngn0.2 $e |- X = ran G $.
    $( The base set of a ring is not empty.  (Contributed by FL,
       24-Jan-2010.) $)
    rngon0 $p |- ( R e. RingOps -> X =/= (/) ) $=
      ( crngo wcel cgr c0 wne rngogrpo grpon0 syl ) AFGBHGCIJABDKBCELM $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d G u x y $.  $d X u x y $.
    $( The range of an internal operation with a left and right identity
       element equals its base set.  (Contributed by FL, 24-Jan-2010.) $)
    rngmgmbs4 $p |- ( ( G : ( X X. X ) --> X /\
      E. u e. X A. x e. X ( ( u G x ) = x /\ ( x G u ) = x ) )
         -> ran G = X ) $=
      ( vy cxp wf cv co wceq wa wral wrex wfo crn r19.12 wcel simpl eqcomd syl
      oveq2 eqeq2d rcla4ev ex syl5 reximdv ralimia anim2i foov sylibr forn ) DD
      FZDCGZBHZAHZCIZUOJZUOUNCIUOJZKZADLBDMZKZULDCNZCODJVAUMUOUNEHZCIZJZEDMZBDM
      ZADLZKVBUTVHUMUTUSBDMZADLVHUSBADDPVIVGADUODQZUSVFBDUSUOUPJZVJVFUSUPUOUQUR
      RSVJVKVFVEVKEUODVCUOJVDUPUOVCUOUNCUAUBUCUDUEUFUGTUHBEADDDCUIUJULDCUKT $.
      $( [22-Dec-2013] $) $( [24-Jan-2010] $)
  $}

  ${
    rnplrnml0.1 $e |- H = ( 2nd ` R ) $.
    rnplrnml0.2 $e |- G = ( 1st ` R ) $.
    $( In a unital ring the domain of the first variable of the addition equals
       the domain of the first variable of the multiplication.  (Contributed by
       FL, 24-Jan-2010.) $)
    rngodm1dm2 $p |- ( R e. RingOps -> dom dom G = dom dom H ) $=
      ( crngo wcel crn cxp wf cdm wceq eqid rngosm cgr wfo wi rngogrpo fdm 3syl
      grpofo fof wa eqtr dmeqd expcom eqcoms syl com12 mpd ) AFGZBHZULIZULCJZBK
      ZKCKZKLZABCULEDULMZNUKBOGUMULBPZUNUQQZABERBULURUAUSUMULBJUOUMLZUTUMULBUBU
      MULBSUNVAUQUNUPUMLVAUQQZUMULCSVBUMUPVAUMUPLZUQVAVCUCUOUPUOUMUPUDUEUFUGUHU
      ITTUJ $.
      $( [24-Jan-2010] $)

    $( In a unital ring the range of the addition equals the domain of the
       first variable of the multiplication.  (Contributed by FL,
       24-Jan-2010.) $)
    rngorn1 $p |- ( R e. RingOps -> ran G = dom dom H ) $=
      ( crngo wcel crn cdm cgr wceq rngogrpo grporndm syl rngodm1dm2 eqtrd ) AF
      GZBHZBIIZCIIQBJGRSKABELBMNABCDEOP $.
      $( [24-Jan-2010] $)

    ${
      $d G x y z $.  $d H x y z $.  $d R x $.
      $( In a unital ring the range of the addition equals the range of the
         multiplication.  (Contributed by FL, 24-Jan-2010.) $)
      rngorn1eq $p |- ( R e. RingOps -> ran G = ran H ) $=
        ( vx vy vz crngo wcel crn cxp wf cv co wceq wa wral wrex eqid cablo w3a
        rngosm rngoi simprr syl rngmgmbs4 syl2anc eqcomd ) AIJZCKZBKZUJULULLULC
        MZFNZGNZCOZUOPUOUNCOUOPQGULRFULSZUKULPABCULEDULTZUCUJBUAJUMQZUPHNZCOUNU
        OUTCOZCOPUNUOUTBOCOUPUNUTCOZBOPUNUOBOUTCOVBVABOPUBHULRGULRFULRZUQQQUQFG
        HABCULEDURUDUSVCUQUEUFGFCULUGUHUI $.
        $( [24-Jan-2010] $)
    $}
  $}

  ${
    $d H x y z $.  $d R x y z $.
    unmnd.1 $e |- H = ( 2nd ` R ) $.
    $( In a unital ring the multiplication is a monoid.  (Contributed by FL,
       24-Jan-2010.) $)
    rngomnd $p |- ( R e. RingOps -> H e. Mnd ) $=
      ( vx vy vz wcel cdm cxp wf cv co wceq wral wa wrex w3a wb eqid cvv xpid11
      crngo cmnd c1st cfv crn rngorn1 biimpri feq23 raleq raleqbi1dv rexeqbi1dv
      mpancom 3anbi123d eqcoms rngosm rngoass ralrimivvva cablo rngoi mpbir3and
      syl simprd c2nd fvex eleq1 mpbiri ismnd1 mp2b sylibr ) AUBGZBHHZVLIZVLBJZ
      DKZEKZBLZFKZBLVOVPVRBLZBLMZFVLNZEVLNZDVLNZVQVPMVPVOBLVPMOZEVLNZDVLPZQZBUC
      GZVKWGAUDUEZUFZWJIZWJBJZVTFWJNZEWJNZDWJNZWDEWJNZDWJPZVKWJVLMWGWLWOWQQRZAW
      IBCWISZUGWRVLWJVLWJMZVNWLWCWOWFWQVMWKMZWTVNWLRXAWTVLWJUAUHVMVLWKWJBUIUMWB
      WNDVLWJWAWMEVLWJVTFVLWJUJUKUKWEWPDVLWJWDEVLWJUJULUNUOVBAWIBWJWSCWJSZUPVKV
      TDEFWJWJWJVOVPVRAWIBWJWSCXBUQURVKVTVOVPVRWILBLVQVOVRBLZWILMVOVPWILVRBLXCV
      SWILMQFWJNEWJNDWJNZWQVKWIUSGWLOXDWQODEFAWIBWJWSCXBUTVCVCVABAVDUEZMZBTGZWH
      WGRCXFXGXETGAVDVEBXETVFVGDEFTBVLVLSVHVIVJ $.
      $( [22-Dec-2013] $) $( [24-Jan-2010] $)
  $}

  ${
    fora1.1 $e |- G e. A $.
    $( In a unital ring the addition is an abelian group.  (Contributed by FL,
       31-Aug-2009.) $)
    rngoablo2lem1 $p |- ( <. G , H >. e. RingOps -> G e. AbelOp ) $=
      ( cop c1st cfv elexi op1st eqcomi rngoablo ) BCEZBLFGBBCBADHIJK $.
      $( [31-Aug-2009] $)
  $}

  $( In a unital ring the addition is an abelian group.  (Contributed by FL,
     31-Aug-2009.) $)
  rngoablo2lem2 $p |- ( ( G e. A /\ <. G , H >. e. RingOps )
         -> G e. AbelOp ) $=
    ( wcel cop crngo cablo cvv wi elex csn wceq opeq1 eleq1d eleq1 imbi12d snex
    cif elimel rngoablo2lem1 dedth syl imp ) BADZBCEZFDZBGDZUDBHDZUFUGIZBAJUHUI
    UHBAAEAEZKZRZCEZFDZULGDZIBUKBULLZUFUNUGUOUPUEUMFBULCMNBULGOPHULCBUKHUJQSTUA
    UBUC $.
    $( [31-Aug-2009] $)

  $( In a unital ring the addition is an abelian group.  (Contributed by FL,
     31-Aug-2009.) $)
  rngoablo2 $p |- ( <. G , H >. e. RingOps -> G e. AbelOp ) $=
    ( cvv wcel cop crngo cablo wbr df-br relrngo brrelexi rngoablo2lem2 mpancom
    sylbir ) ACDZABEFDZAGDPABFHOABFIABFJKNCABLM $.
    $( [31-Aug-2009] $)

  ${
    uridm.1 $e |- H = ( 2nd ` R ) $.
    uridm.2 $e |- X = ran ( 1st ` R ) $.
    ${
      uridm.3 $e |- U = ( Id ` H ) $.
      $( The unit of a ring is an identity element for the multiplication.
         (Contributed by FL, 18-Feb-2010.) $)
      rngoidmlem $p |- ( ( R e. RingOps /\ A e. X ) ->
        ( ( U H A ) = A /\ ( A H U ) = A ) ) $=
        ( crngo wcel co wceq wa wi crn cmnd cmagm cexid eqid ex cin rngomnd cfv
        mndmgmid cmpidelt 3syl c1st rngorn1eq eqtr simpl eleq2d imbi1d syl mpan
        wb mpcom mpbird imp ) BIJZAEJZCADKALACDKALMZUSUTVANZADOZJZVANZUSDPJDQRU
        AJZVEBDFUBDUDVFVDVAACDVCVCSHUETUFBUGUCZOZVCLZUSVBVEUOZBVGDFVGSUHEVHLZVI
        USVJNZGVKVIMEVCLZVLEVHVCUIVMUSVJVMUSMZUTVDVAVNEVCAVMUSUJUKULTUMUNUPUQUR
        $.
        $( [18-Feb-2010] $)

      $( The unit of a ring is an identity element for the multiplication.
         (Contributed by FL, 18-Apr-2010.) $)
      rngolidm $p |- ( ( R e. RingOps /\ A e. X ) -> ( U H A ) = A ) $=
        ( crngo wcel wa co wceq rngoidmlem simpld ) BIJAEJKCADLAMACDLAMABCDEFGH
        NO $.
        $( [18-Apr-2010] $)
    $}

    ${
      uridm2.2 $e |- U = ( Id ` H ) $.
      $( The unit of a ring is an identity element for the multiplication.
         (Contributed by FL, 18-Apr-2010.) $)
      rngoridm $p |- ( ( R e. RingOps /\ A e. X ) -> ( A H U ) = A ) $=
        ( crngo wcel wa co wceq rngoidmlem simprd ) BIJAEJKCADLAMACDLAMABCDEFGH
        NO $.
        $( [18-Apr-2010] $)
    $}
  $}

  ${
    $d A x $.  $d R x $.  $d X x $.
    on1el3.1 $e |- G = ( 1st ` R ) $.
    on1el3.2 $e |- X = ran G $.
    $( The only unital ring with a base set consisting in one element is the
       zero ring.  (Contributed by FL, 13-Feb-2010.) $)
    rngosn3 $p |- ( ( R e. RingOps /\ A e. B ) -> ( X = { A } <-> R =
       <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ) ) $=
      ( wcel crngo csn wceq cop wi cxp wf crn cvv wa syl6bi ax-mp vx wb cv sneq
      eqeq2d opeq12 anidms mpancom sneqd opeq12d bibi12d imbi2d xpid11 c2nd cfv
      c1st eqid rngosm cgr wfo rngogrpo rneqi eqtri fof 3syl w3a feq23 vex xpsn
      grpofo feq2 fconst2 biimpi opex syl6eq anim12d syl5bi pm2.43i exp3a com13
      ex 3imp a1i cdm wrel relrngo relssdmrn xpss sstri sseli jctild eqopi syl6
      3expd mp2d syl5bir fveq2 snex op1st eqtr mpan2 rneq syl5eq eqcomi 3eqtr3g
      wss rnsnop impbid1 vtoclg impcom ) ABHCIHZEAJZKZCAALZALZJZXPLZKZUBZXKEUAU
      CZJZKZCXTXTLZXTLZJZYELZKZUBZMXKXSMUAABXTAKZYHXSXKYIYBXMYGXRYIYAXLEXTAUDUE
      YIYFXQCYIYEXPYEXPYIYDXOYCXNKZYIYDXOKYIYJXTXTAAUFUGYCXTXNAUFUHUIZYKUJUEUKU
      LXKYBYGYBEENZYAYANZKZXKYGEYAUMZXKYLECUNUOZOZYLECUPUOZOZYNYGMCDYPEFYPUQGUR
      XKYRUSHYLEYRUTYSCYRYRUQVAYREEDPZYRPZGDYRFVBVCVJYLEYRVDVEXKYQYSYNYGXKYQYSY
      NVFZCQQNZHZYRYEKZYPYEKZRZRYGXKUUBUUGUUDUUBUUGMXKYQYSYNUUGYNYSYQUUGYNYSYQU
      UGYNYSYQRUUGMZYNYBYNUUHYOYNYBUUHYNYBRZYSUUEYQUUFUUIYSYMYAYROZUUEYLEYMYAYR
      VGYMYCJZKZUUJUUEMXTXTUAVHZUUMVIZUULUUJUUKYAYROZUUEYMUUKYAYRVKUUOYRUUKYANZ
      YEUUOYRUUPKUUKXTYRUUMVLVMYCXTXTXTVNZUUMVIZVOSTSUUIYQYMYAYPOZUUFYLEYMYAYPV
      GUULUUSUUFMUUNUULUUSUUKYAYPOZUUFYMUUKYAYPVKUUTYPUUPYEUUTYPUUPKUUKXTYPUUMV
      LVMUURVOSTSVPWAVQVRVSVTWBWCIUUCCIIWDZIPZNZUUCIWEIUVCXFWFIWGTUVAUVBWHWIWJW
      KCYEYEQQWLWMWNWOWPYGYRYFUPUOZKZUUEYBCYFUPWQUVEUVDYEKUUEYEYEYDWRWSYRUVDYEW
      TXAUUEUUAYEPEYAYRYEXBEUUADYRKZEUUAKFUVFEYTUUAGDYRXBXCTXDYCXTUUQUUMXGXEVEX
      HXIXJ $.
      $( [13-Feb-2010] $)

    ${
      $d A x $.  $d R x $.  $d X x $.
      $( The only unital ring with one element is the zero ring.  (Contributed
         by FL, 14-Feb-2010.) $)
      rngosn4 $p |- ( ( R e. RingOps /\ A e. X ) -> ( X ~~ 1o <-> R =
       <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ) ) $=
        ( vx wcel c1o cen cop csn wceq wi wa ex syl c1st eqtr crn wbr wb opeq12
        crngo anidms mpancom sneqd opeq12d eqeq2d bibi2d imbi2d en1eqsn rngosn3
        cv biimpd com13 pm2.43a com23 imp cfv fveq2 snex op1st rneq opex rnsnop
        vex ensn1 breq1 mpbiri sylancl sylancr mpsyl com12 adantr impbid impcom
        a1d vtoclga ) ADHBUDHZDIJUAZBAAKZAKZLZWDKZMZUBZVTWABGUNZWHKZWHKZLZWKKZM
        ZUBZNVTWGNGADWHAMZWNWGVTWOWMWFWAWOWLWEBWOWKWDWKWDWOWJWCWIWBMZWOWJWCMWOW
        PWHWHAAUCUEWIWHWBAUCUFUGZWQUHUIUJUKWHDHZVTWNWRVTOWAWMWRVTWAWMNWRWAVTWMW
        AWRVTWMNZWRWAWRWSNZWRWAODWHLZMZWTWHDULVTWRXBWMVTWRXBWMNVTWROXBWMWHDBCDE
        FUMUOPUPQPUQURUSWRWMWANVTWMWRWAWMBRUTZWLRUTZMZXDWKMZWRWANZBWLRVAWKWKWJV
        BVCXEXFOXCWKMZXGXCXDWKSCXCMZXHXCTZWKTZMZXGEXCWKVDXIDCTZMZXMXJMZXLXGNZFC
        XCVDXNXOODXJMZXPDXMXJSXQXLXGXQXLODXKMZXKXAMZXGDXJXKSWIWHWHWHVEGVGZVFXRX
        SOXBXGDXKXASXBWAWRXBWAXAIJUAWHXTVHDXAIJVIVJVRQVKPQVLVMQVKVNVOVPPVSVQ $.
        $( [14-Feb-2010] $)
    $}

    ${
      on1el3.3 $e |- Z = ( Id ` G ) $.
      $( The only unital ring with one element is the zero ring.  (Contributed
         by FL, 15-Feb-2010.) $)
      rngosn6 $p |- ( R e. RingOps -> ( X ~~ 1o <-> R =
        <. { <. <. Z , Z >. , Z >. } , { <. <. Z , Z >. , Z >. } >. ) ) $=
        ( crngo wcel c1o cen wbr cop csn wceq wb rngo0cl rngosn4 mpdan ) AHIDCI
        CJKLADDMDMNZTMOPABCDEFGQDABCEFRS $.
        $( [15-Feb-2010] $)
    $}
  $}

  ${
    $d H u x $.  $d X u x $.
    ring1cl.1 $e |- X = ran ( 1st ` R ) $.
    ring1cl.2 $e |- H = ( 2nd ` R ) $.
    ring1cl.3 $e |- U = ( Id ` H ) $.
    $( The unit of a ring belongs to the base set.  (Contributed by FL,
       12-Feb-2010.) $)
    rngo1cl $p |- ( R e. RingOps -> U e. X ) $=
      ( crngo wcel c2nd cfv crn cmagm cexid wa cmnd syl eqid cgi wceq mndisexid
      cin rngomnd eleq1i mndismgm sylbi elin sylibr fveq2i eqtri iorlid c1st wb
      jca rngorn1eq eqtr eleq2d sylancr mpbird ) AHIZBDIZBAJKZLZIZUTVBMNUBIZVDU
      TVBMIZVBNIZOZVEUTCPIZVHACFUCVIVBPIZVHCVBPFUDVJVFVGVBUEVBUAUNUFQVBMNUGUHBV
      BVCVCRBCSKVBSKGCVBSFUIUJUKQUTDAULKZLZTZVLVCTZVAVDUMEAVKVBVBRVKRUOVMVNODVC
      BDVLVCUPUQURUS $.
      $( [12-Feb-2010] $)
  $}

  ${
    $d R x $.  $d U x $.  $d X x $.  $d Z x $.
    uznzr.1 $e |- G = ( 1st ` R ) $.
    uznzr.2 $e |- H = ( 2nd ` R ) $.
    uznzr.3 $e |- Z = ( Id ` G ) $.
    uznzr.4 $e |- U = ( Id ` H ) $.
    uznzr.5 $e |- X = ran G $.
    $( In a unital ring the zero equals the unity iff the ring is the zero
       ring.  (Contributed by FL, 14-Feb-2010.) $)
    rngoueqz $p |- ( R e. RingOps -> ( X ~~ 1o <-> U = Z ) ) $=
      ( vx wcel c1o wceq wi wa syl ex wral cen wbr rngo0cl csn en1eqsn crn c1st
      crngo rneqi rngo1cl eleq2 biimpd elsni syl6com eqcomi syl5com com23 mpcom
      cfv eleq2s c0 wne rngon0 cv oveq2 ralrimivw rngorz ralrimiva eqtri r19.26
      rngoridm eqtr eqcoms imp31 ralimi eqsn ensn1g breq1 syl5ibr syl6bir com3l
      co sylbir com24 mpd com13 impbid ) AUHMZENUAUBZBFOZFEMZWHWIWJPACEFGKIUCZW
      KWIWHWJWKWIWHWJPWKWIQEFUDZOZWHWJFEUEWHBCUFZMWNWJPZABDWOCAUGUSZGUIZHJUJWPB
      EWOWNBEMZBWMMZWJWNWSWTEWMBUKULBFUMUNEWOKUOUTRUPSUQUREVAVBZWHWJWIPACEGKVCW
      JWHXAWIWJLVDZBDWBZXBFDWBZOZLETZWHXAWIPZWJXELEBFXBDVEVFWHXDFOZLETZXFXGPZWH
      XHLEXBACDEFIKGHVGVHXCXBOZLETZWHXIXJPWHXKLEXBABDEHEWOWQUFKWRVIJVKVHXLXFXIW
      HXGXLXFXIWHXGPZPZXLXFQXKXEQZLETZXNXKXELEVJXPXIXMXPXIQXOXHQZLETZXMXOXHLEVJ
      XRXBFOZLETZXMXQXSLEXKXEXHXSXEXHXSPZPXBXCXBXCOZXEYAYBXEQXBXDOZYAXBXCXDVLYC
      XHXSXBXDFVLSRSVMVNVOXAXTWHWIXAXTWNWHWIPLEFVPWHWIWNWMNUAUBZWHWKYDWLFEVQREW
      MNUAVRVSVTWARWCSWCSWDURWEUPWFURWG $.
      $( [14-Feb-2010] $)
  $}

  ${
    $d G g h $.  $d H g h $.  $d x y $.
    $( The predicate "is a division ring".  (Contributed by FL, 6-Sep-2009.) $)
    isdivrngo $p |- ( H e. A -> ( <. G , H >. e. DivRingOps
     <-> ( <. G , H >. e. RingOps /\ ( H |` (
       ( ran G \ { ( Id ` G ) } ) X. ( ran G \ { ( Id ` G ) } ) ) )
         e. GrpOp ) ) ) $=
      ( vx vy vg vh wcel cop cdrng crngo crn cgi cfv csn cres cgr wa cv eleq1d
      cdif cxp cvv wbr df-br df-drngo relopabi brrelexi sylbir anim1i rngoablo2
      ancoms cablo elex ad2antrl simpl copab eleq2i wceq opeq1 rneq fveq2 sneqd
      syl jca difeq12d xpeq12d reseq2d anbi12d reseq1 opelopabg syl5bb pm5.21nd
      opeq2 ) CAHZBCIZJHZVPKHZCBLZBMNZOZUAZWBUBZPZQHZRZBUCHZVORZVQVOWHVQWGVOVQB
      CJUDWGBCJUEBCJDSZESZIKHWJWILWIMNOUAZWKUBPQHRDEJDEUFUGUHUIUJULVOWFRWGVOVRW
      GVOWEVRBUMHWGBCUKBUMUNVDUOVOWFUPVEVQVPFSZGSZIZKHZWMWLLZWLMNZOZUAZWSUBZPZQ
      HZRZFGUQZHWHWFJXDVPFGUFURXCBWMIZKHZWMWCPZQHZRWFFGBCUCAWLBUSZWOXFXBXHXIWNX
      EKWLBWMUTTXIXAXGQXIWTWCWMXIWSWBWSWBXIWPVSWRWAWLBVAXIWQVTWLBMVBVCVFZXJVGVH
      TVIWMCUSZXFVRXHWEXKXEVPKWMCBVNTXKXGWDQWMCWCVJTVIVKVLVM $.
      $( [6-Sep-2009] $)
  $}

  ${
    zrdivrng.1 $e |- A e. _V $.
    $( The zero ring is not a division ring.  (Contributed by FL,
       24-Jan-2010.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    zrdivrng $p |- -.
      <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >.
         e. DivRingOps $=
      ( cop csn cdrng wcel c0 cgr 0ngrp crn cgi cfv cdif cres opex rnsnop gidsn
      cxp eqtri cvv sneqi difeq12i difid xpeq2i reseq2i res0 crngo wa isdivrngo
      xp0 wb snex ax-mp simprbi syl5eqelr mto ) AACZACZDZUSCZEFZGHFIVAGUSUSJZUS
      KLZDZMZVERZNZHVGUSGNGVFGUSVFVEGRGVEGVEVEADZVHMGVBVHVDVHUQAAAOBPVCAABQUAUB
      VHUCSUDVEUJSUEUSUFSVAUTUGFZVGHFZUSTFVAVIVJUHUKURULTUSUSUIUMUNUOUP $.
      $( [15-Dec-2013] $) $( [24-Jan-2010] $)
  $}

  ${
    dvrunz.1 $e |- G = ( 1st ` R ) $.
    dvrunz.2 $e |- H = ( 2nd ` R ) $.
    dvrunz.3 $e |- X = ran G $.
    dvrunz.4 $e |- Z = ( Id ` G ) $.
    dvrunz.5 $e |- U = ( Id ` H ) $.
    $( In a division ring the unit is different from the zero.  (Contributed by
       FL, 14-Feb-2010.) $)
    dvrunz $p |- ( R e. DivRingOps -> U =/= Z ) $=
      ( cdrng wcel cop csn wn cgi wceq wb syl wne cfv cvv fvex eqeltri zrdivrng
      c1o cen wbr crngo cdif cxp cres cgr drngoi simpld rngoueqz rngosn6 biimpd
      wi eleq1 syl6bi pm2.43a sylbird necon3bd mpi ) ALMZFFNFNOZVHNZLMZPBFUAFFC
      QUBUCJCQUDUEUFVGVJBFVGBFRZEUGUHUIZVJVGAUJMZVLVKSVGVMDEFOUKZVNULUMUNMACDEF
      GHIJUOUPZABCDEFGHJKIUQTVLVGVJVGVLAVIRZVGVJUTVGVMVLVPSVOACEFGIJURTVPVGVJAV
      ILVAUSVBVCVDVEVF $.
      $( [15-Dec-2013] $) $( [14-Feb-2010] $)
  $}

