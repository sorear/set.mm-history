$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Group-like structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Magma $.

  $( Extend class notation with the class of all magmas. $)
  cmagm $a class Magma $.

  ${
    $d g t $.
    $( A magma is a binary internal operation. $)
    df-mgm $a |- Magma = { g | E. t g : ( t X. t ) --> t } $.
  $}

  ${
    $d G g t $.  $d X t $.
    ismgm.1 $e |- X = dom dom G $.
    $( The predicate "is a magma".  (Contributed by FL, 2-Nov-2009.) $)
    ismgm $p |- ( G e. A -> ( G e. Magma <-> G : ( X X. X ) --> X ) ) $=
      ( vt vg wcel cmagm cv cdm wceq cxp wf wa wex c0 wi dmeq wb cvv exbidv f00
      feq1 df-mgm elab2g dm0 dmeqi eqtri syl6req syl adantr sylbi xpeq12 anidms
      feq23 mpancom eqeq1 imbi12d mpbiri wn fdm wne df-ne sylbir eqeq1d biimpcd
      dmxp eqcoms 3syl com12 pm2.61i pm4.71ri exbii syl6bb dmexg eqcomi xpeq12i
      feq23i ceqsexgv bitrd ) BAGZBHGZEIZBJZJZKZWCWCLZWCBMZNZEOZCCLZCBMZWAWBWHE
      OZWJWGWCFIZMZEOWMFBHAWNBKWOWHEWGWCWNBUCUAEFUDUEWHWIEWHWFWCPKZWHWFQZWPWQPP
      LZPBMZPWEKZQWSBPKZWRPKZNWTWRBUBXAWTXBXAWDPJZKZWTBPRXDWEXCJZPWDXCRXEXCPXCP
      UFUGUFUHUIUJUKULWPWHWSWFWTWGWRKZWPWHWSSWPXFWCPWCPUMUNWGWCWRPBUOUPWCPWEUQU
      RUSWHWPUTZWFWHWDWGKWEWGJZKXGWFQZWGWCBVAWDWGRXIXHWEXGXHWEKWFXGXHWCWEXGWCPV
      BXHWCKWCPVCWCWCVGVDVEVFVHVIVJVKVLVMVNWAWDTGWETGWJWLSBAVOWDTVOWHWLEWETWFWH
      WEWELZWEBMZWLWGXJKZWFWHXKSWFXLWCWEWCWEUMUNWGWCXJWEBUOUPXJWEWKCBWECWECCWED
      VPZXMVQXMVRVNVSVIVT $.
      $( [2-Nov-2009] $)
  $}

  ${
    clmgm.1 $e |- X = dom dom G $.
    $( Closure of a magma.  (Contributed by FL, 14-Sep-2010.) $)
    clmgm $p |- ( ( G e. Magma /\ A e. X /\ B e. X ) -> ( A G B ) e. X ) $=
      ( cmagm wcel co wi cxp wf ismgm fovrn 3exp syl6bi pm2.43i 3imp ) CFGZADGZ
      BDGZABCHDGZRSTUAIIZRRDDJDCKZUBFCDELUCSTUAABDDDCMNOPQ $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d G u x y $.  $d X u x y $.
    opidon.1 $e |- X = dom dom G $.
    $( An operation with a left and right identity element is onto.
       (Contributed by FL, 2-Nov-2009.) $)
    opidon $p |- ( G e. ( Magma i^i ExId ) ->
      G : ( X X. X ) -onto-> X ) $=
      ( vy vu vx cmagm cexid cin wcel cxp wf cv co wceq wrex wral wfo sseli syl
      inss1 ismgm ibi wa inss2 isexid biimpd sylc simpl ralimi oveq2 id eqeq12d
      rcla4v eqcom eqeq1d syl5bb rcla4ev ex syld syl5 impcom ralrimiva sylanbrc
      reximdv foov ) AGHIZJZBBKZBALZDMZEMZFMZANZOZFBPZEBPZDBQZVIBARVHAGJZVJVGGA
      GHUASVSVJGABCUBUCTVHVNVMOZVMVLANVMOZUDZFBQZEBPZVRVHAHJZWEWDVGHAGHUESZWFWE
      WEWDEFHABCUFUGUHWDVQDBVKBJZWDVQWGWCVPEBWCVTFBQZWGVPWBVTFBVTWAUIUJWGWHVLVK
      ANZVKOZVPVTWJFVKBVMVKOZVNWIVMVKVMVKVLAUKZWKULUMUNWGWJVPVOWJFVKBVOVNVKOWKW
      JVKVNUOWKVNWIVKWLUPUQURUSUTVAVEVBVCTEFDBBBAVFVD $.
      $( [22-Dec-2013] $) $( [2-Nov-2009] $)
  $}

  $( Range of an operation with a left and right identity element.
     (Contributed by FL, 2-Nov-2009.) $)
  rngopid $p |- ( G e. ( Magma i^i ExId ) -> ran G = dom dom G ) $=
    ( cmagm cexid cin wcel cdm cxp wfo crn wceq eqid opidon forn syl ) ABCDEAFF
    ZOGZOAHAIOJAOOKLPOAMN $.
    $( [2-Nov-2009] $)

  ${
    opidon2.1 $e |- X = ran G $.
    $( An operation with a left and right identity element is onto.
       (Contributed by FL, 2-Nov-2009.) $)
    opidon2 $p |- ( G e. ( Magma i^i ExId ) ->
      G : ( X X. X ) -onto-> X ) $=
      ( cmagm cexid cin wcel cdm cxp wfo eqid opidon wceq crn syl5req wb xpeq12
      forn anidms syl foeq2 foeq3 bitrd biimpd mpcom ) ADEFGAHHZUFIZUFAJZBBIZBA
      JZAUFUFKLUFBMZUHUJUHBANUFCUGUFAROUKUHUJUKUHUIUFAJZUJUKUGUIMZUHULPUKUMUFBU
      FBQSUGUIUFAUATUFBUIAUBUCUDUET $.
      $( [2-Nov-2009] $)

  $}

  ${
    $d G u x $.  $d X u x $.
    isexid2.1 $e |- X = ran G $.
    $( If ` G e. ( Magma i^i ExId ) ` then it has a left and right identity
       element that belongs to the range of the operation.  (Contributed by FL,
       12-Dec-2009.) $)
    isexid2 $p |- ( G e. ( Magma i^i ExId ) -> E. u e. X A. x e. X
      ( ( u G x ) = x /\ ( x G u ) = x ) ) $=
      ( crn wceq cmagm cexid cin wcel cv co wa wral wrex cdm raleq rexeqbi1dv
      wi rngopid elin isexid ibi adantl sylbi eqeq2 imbi12d syl5ibr mpcom com12
      eqid a1d sylibrd ax-mp ) DCFZGZCHIJKZBLZALZCMUTGUTUSCMUTGNZADOZBDPZTEUQUR
      VAAUPOZBUPPZVCURUQVEUPCQQZGZURUQVETZCUAURVHVGDVFGZVAAVFOZBVFPZTZURCHKZCIK
      ZNVLCHIUBVNVLVMVNVKVIVNVKBAICVFVFULUCUDUMUEUFVGUQVIVEVKUPVFDUGVDVJBUPVFVA
      AUPVFRSUHUIUJUKVBVDBDUPVAADUPRSUNUO $.
      $( [22-Dec-2013] $) $( [12-Dec-2009] $)
  $}

  ${
    $d G u x y $.  $d X u x y $.
    exidu1.1 $e |- X = ran G $.
    $( Unicity of the left and right identity element of a magma when it
       exists.  (Contributed by FL, 12-Dec-2009.) $)
    exidu1 $p |- ( G e. ( Magma i^i ExId ) ->
      E! u e. X A. x e. X ( ( u G x ) = x /\ ( x G u ) = x ) ) $=
      ( vy wcel cv co wceq wa wral ralimi oveq2 id eqeq12d rcla4v oveq1 eqeq1d
      syl5 cmagm cexid cin wrex wreu isexid2 simpl simpr im2anan9r eqtr2 eqcomd
      wi syl6 rgen2a a1i anbi12d ralbidv reu4 sylanbrc ) CUAUBUCGZBHZAHZCIZVBJZ
      VBVACIZVBJZKZADLZBDUDVHFHZVBCIZVBJZVBVICIZVBJZKZADLZKZVAVIJZULZFDLBDLZVHB
      DUEABCDEUFVSUTVRBFDVADGZVIDGZKVPVAVICIZVIJZWBVAJZKZVQWAVHWCVTVOWDVHVDADLW
      AWCVGVDADVDVFUGMVDWCAVIDVBVIJZVCWBVBVIVBVIVACNWFOPQTVOVMADLVTWDVNVMADVKVM
      UHMVMWDAVADVBVAJZVLWBVBVAVBVAVICRWGOPQTUIWEVIVAWBVIVAUJUKUMUNUOVHVOBFDVQV
      GVNADVQVDVKVFVMVQVCVJVBVAVIVBCRSVQVEVLVBVAVIVBCNSUPUQURUS $.
      $( [22-Dec-2013] $) $( [12-Dec-2009] $)
  $}

  ${
    $d G u x $.  $d X u x $.
    idrval.1 $e |- X = ran G $.
    idrval.2 $e |- U = ( Id ` G ) $.
    $( The value of the identity element.  (Contributed by FL, 12-Dec-2009.)
       (Revised by Mario Carneiro, 22-Dec-2013.) $)
    idrval $p |- ( G e. A -> U = ( iota_ u e. X
                   A. x e. X ( ( u G x ) = x /\ ( x G u ) = x ) ) ) $=
      ( wcel cgi cfv cv co wceq wa wral crio gidval syl5eq ) ECIDEJKBLZALZEMUAN
      UATEMUANOAFPBFQHABECFGRS $.
      $( [22-Dec-2013] $) $( [12-Dec-2009] $)
  $}

  ${
    $d G u x $.  $d X u x $.
    iorlid.1 $e |- X = ran G $.
    iorlid.2 $e |- U = ( Id ` G ) $.
    $( A magma right and left identity belongs to the underlying set of the
       operation.  (Contributed by FL, 12-Dec-2009.) $)
    iorlid $p |- ( G e. ( Magma i^i ExId ) -> U e. X ) $=
      ( vu vx cmagm cexid cin wcel cv co wceq wa wral crio idrval wreu exidu1
      riotacl syl eqeltrd ) BHIJZKZAFLZGLZBMUGNUGUFBMUGNOGCPZFCQZCGFUDABCDERUEU
      HFCSUICKGFBCDTUHFCUAUBUC $.
      $( [22-Dec-2013] $) $( [12-Dec-2009] $)
  $}

  ${
    $d A x $.  $d G u x y $.  $d U u x y $.  $d X u x y $.
    cmpidelt.1 $e |- X = ran G $.
    cmpidelt.2 $e |- U = ( Id ` G ) $.
    $( A magma right and left identity element keeps the other elements
       unchanged.  (Contributed by FL, 12-Dec-2009.) $)
    cmpidelt $p |- ( ( G e. ( Magma i^i ExId ) /\ A e. X ) ->
      ( ( U G A ) = A /\ ( A G U ) = A ) ) $=
      ( vx vu vy wcel cv co wceq wa wral oveq1 eqeq1d oveq2 anbi12d eqeq12d cin
      cmagm cexid crio idrval eqcomd wreu wb iorlid exidu1 a17d ralbidv riota2f
      ax-17 syl2anc mpbird id rcla4cva sylan ) CUBUCUAZJZBGKZCLZVBMZVBBCLZVBMZN
      ZGDOZADJBACLZAMZABCLZAMZNZVAVHHKZVBCLZVBMZVBVNCLZVBMZNZGDOZHDUDZBMZVABWAG
      HUTBCDEFUEUFVABDJZVTHDUGVHWBUHBCDEFUIGHCDEUJVTVHHIDBIKBJHUNWCVHHUKVNBMZVS
      VGGDWDVPVDVRVFWDVOVCVBVNBVBCPQWDVQVEVBVNBVBCRQSULUMUOUPVGVMGADVBAMZVDVJVF
      VLWEVCVIVBAVBABCRWEUQZTWEVEVKVBAVBABCPWFTSURUS $.
      $( [22-Dec-2013] $) $( [12-Dec-2009] $)
  $}

  $c SemiGrp $.

  $( Extend class notation with the class of all semi-groups. $)
  csem $a class SemiGrp $.

  $( A semi-group is an associative magma. $)
  df-sgr $a |- SemiGrp = ( Magma i^i Ass ) $.

  $( A semi-group is a magma.  (Contributed by FL, 2-Nov-2009.) $)
  smgrpismgm $p |- ( G e. SemiGrp -> G e. Magma ) $=
    ( cmagm wcel cass cin csem elin simplbi df-sgr eleq2s ) ABCZABDEZFALCKADCAB
    DGHIJ $.
    $( [2-Nov-2009] $)

  $( A semi-group is associative.  (Contributed by FL, 2-Nov-2009.) $)
  smgrpisass $p |- ( G e. SemiGrp -> G e. Ass ) $=
    ( cass wcel cmagm cin csem elin simprbi df-sgr eleq2s ) ABCZADBEZFALCADCKAD
    BGHIJ $.
    $( [2-Nov-2009] $)

  ${
    $d G x y z $.  $d X x y z $.
    issmgrp.1 $e |- X = dom dom G $.
    $( The predicate "is a semi-group".  (Contributed by FL, 2-Nov-2009.) $)
    issmgrp $p |- ( G e. A -> ( G e. SemiGrp <-> ( G : ( X X. X ) --> X /\
     A. x e. X A. y e. X A. z e. X ( ( x G y ) G z ) = ( x G ( y G z ) ) ) ) )
        $=
      ( csem wcel cmagm cass cin cxp wf cv co wceq wral wa syl5bb df-sgr eleq2i
      elin ismgm isass anbi12d ) EHIEJKLZIZEDIZFFMFENZAOZBOZEPCOZEPUKULUMEPEPQC
      FRBFRAFRZSZHUGEUAUBUHEJIZEKIZSUIUOEJKUCUIUPUJUQUNDEFGUDABCDEFGUEUFTT $.
      $( [2-Nov-2009] $)
  $}

  ${
    $d G x y z $.  $d X x y z $.
    smgrpmgm.1 $e |- X = dom dom G $.
    $( A semi-group is a magma.  (Contributed by FL, 2-Nov-2009.) $)
    smgrpmgm $p |- ( G e. SemiGrp -> G : ( X X. X ) --> X ) $=
      ( vx vy vz csem wcel cxp wf cv co wceq wral issmgrp simpl syl6bi pm2.43i
      wa ) AGHZBBIBAJZTTUADKZEKZALFKZALUBUCUDALALMFBNEBNDBNZSUADEFGABCOUAUEPQR
      $.
      $( [2-Nov-2009] $)
  $}

  ${
    $d G x y z $.  $d X x y z $.
    smgrpass.1 $e |- X = dom dom G $.
    $( A semi-group is associative.  (Contributed by FL, 2-Nov-2009.) $)
    smgrpass $p |- ( G e. SemiGrp ->
      A. x e. X A. y e. X A. z e. X ( ( x G y ) G z ) = ( x G ( y G z ) ) ) $=
      ( csem wcel cv co wceq wral cxp wf wa issmgrp simpr syl6bi pm2.43i ) DGHZ
      AIZBIZDJCIZDJUAUBUCDJDJKCELBELAELZTTEEMEDNZUDOUDABCGDEFPUEUDQRS $.
      $( [2-Nov-2009] $)
  $}

  $c Mnd $.

  $( Extend class notation with the class of all monoids. $)
  cmnd $a class Mnd $.

  $( A monoid is a semi-group with an identity element. $)
  df-mnd $a |- Mnd = ( SemiGrp i^i ExId ) $.

  $( A monoid is a semi-group.  (Contributed by FL, 2-Nov-2009.) $)
  mndissmgrp $p |- ( G e. Mnd -> G e. SemiGrp ) $=
    ( csem wcel cexid cin cmnd elin simplbi df-mnd eleq2s ) ABCZABDEZFALCKADCAB
    DGHIJ $.
    $( [2-Nov-2009] $)

  $( A monoid has an identity element.  (Contributed by FL, 2-Nov-2009.) $)
  mndisexid $p |- ( G e. Mnd -> G e. ExId ) $=
    ( cexid wcel csem cin cmnd elin simprbi df-mnd eleq2s ) ABCZADBEZFALCADCKAD
    BGHIJ $.
    $( [2-Nov-2009] $)

  $( A monoid is a magma.  (Contributed by FL, 2-Nov-2009.) $)
  mndismgm $p |- ( G e. Mnd -> G e. Magma ) $=
    ( cmnd wcel csem cmagm mndissmgrp smgrpismgm syl ) ABCADCAECAFAGH $.
    $( [2-Nov-2009] $)

  $( A monoid is a magma with an identity element.  (Contributed by FL,
     18-Feb-2010.) $)
  mndmgmid $p |- ( G e. Mnd -> G e. ( Magma i^i ExId ) ) $=
    ( cmnd wcel cmagm cexid cin mndismgm mndisexid elin sylanbrc ) ABCADCAECADE
    FCAGAHADEIJ $.
    $( [18-Feb-2010] $)

  ${
    $d G x y $.  $d X x y $.
    ismond.1 $e |- X = dom dom G $.
    $( The predicate "is a monoid".  (Contributed by FL, 2-Nov-2009.) $)
    ismnd $p |- ( G e. A -> ( G e. Mnd
     <-> ( G e. SemiGrp /\ E. x e. X A. y e. X
      ( ( x G y ) = y /\ ( y G x ) = y ) ) ) ) $=
      ( cmnd wcel csem cexid cin cv co wceq wa wral wrex df-mnd eleq2i syl5bb
      elin isexid anbi2d ) DGHDIJKZHZDCHZDIHZALZBLZDMUINUIUHDMUINOBEPAEQZOZGUDD
      RSUEUGDJHZOUFUKDIJUAUFULUJUGABCDEFUBUCTT $.
      $( [22-Dec-2013] $) $( [2-Nov-2009] $)
  $}

  ${
    $d G x y z $.  $d X x y z $.
    ismnd1.1 $e |- X = dom dom G $.
    $( The predicate "is a monoid".  (Contributed by FL, 2-Nov-2009.) $)
    ismnd1 $p |- ( G e. A -> ( G e. Mnd <->
      ( G : ( X X. X ) --> X /\
        A. x e. X A. y e. X A. z e. X
          ( ( x G y ) G z ) = ( x G ( y G z ) ) /\
        E. x e. X A. y e. X
         ( ( x G y ) = y /\ ( y G x ) = y ) ) ) ) $=
      ( wcel cmnd csem cv co wceq wa wral wrex cxp wf w3a ad2antrl ismnd simprr
      smgrpmgm smgrpass 3jca 3simpa issmgrp syl5ibr imp simpr3 impbida bitrd
      jca ) EDHZEIHEJHZAKZBKZELZUQMUQUPELUQMNBFOAFPZNZFFQFERZURCKZELUPUQVBELELM
      CFOBFOAFOZUSSZABDEFGUAUNUTVDUNUTNVAVCUSUOVAUNUSEFGUCTUOVCUNUSABCEFGUDTUNU
      OUSUBUEUNVDNUOUSUNVDUOVDUOUNVAVCNVAVCUSUFABCDEFGUGUHUIUNVAVCUSUJUMUKUL $.
      $( [22-Dec-2013] $) $( [2-Nov-2009] $)
  $}

  ${
    $d G x y z $.  $d X x y z $.
    ismnd2.1 $e |- X = ran G $.
    $( The predicate "is a monoid".  (Contributed by FL, 2-Nov-2009.) $)
    ismnd2 $p |- ( G e. A -> ( G e. Mnd <->
      ( G : ( X X. X ) --> X /\
        A. x e. X A. y e. X A. z e. X
          ( ( x G y ) G z ) = ( x G ( y G z ) ) /\
        E. x e. X A. y e. X
         ( ( x G y ) = y /\ ( y G x ) = y ) ) ) ) $=
      ( wcel cdm wceq cxp wf cv co wral wrex w3a wi a1i wb cmnd crn cmagm cexid
      wa cin mndmgmid rngopid syl syl5eq fdm dmeqd dmxpid syl6req 3ad2ant1 eqid
      ismnd1 xpid11 biimpri feq23 mpancom raleq raleqbi1dv rexeqbi1dv 3anbi123d
      bibi2d syl5ibrcom pm5.21ndd ) EDHZFEIZIZJZEUAHZFFKZFELZAMZBMZENZCMZENVPVQ
      VSENENJZCFOZBFOZAFOZVRVQJVQVPENVQJUEZBFOZAFPZQZVMVLRVIVMFEUBZVKGVMEUCUDUF
      HWHVKJEUGEUHUIUJSWGVLRVIVOWCVLWFVOVKVNIFVOVJVNVNFEUKULFUMUNUOSVIVMWGTVLVM
      VKVKKZVKELZVTCVKOZBVKOZAVKOZWDBVKOZAVKPZQZTABCDEVKVKUPUQVLWGWPVMVLVOWJWCW
      MWFWOVNWIJZVLVOWJTWQVLFVKURUSVNFWIVKEUTVAWBWLAFVKWAWKBFVKVTCFVKVBVCVCWEWN
      AFVKWDBFVKVBVDVEVFVGVH $.
      $( [22-Dec-2013] $) $( [2-Nov-2009] $)
  $}

  ${
    $d G w x y z $.
    $( A group is a monoid.  (Contributed by FL, 2-Nov-2009.) $)
    grpomnd $p |- ( G e. GrpOp -> G e. Mnd ) $=
      ( vx vy vz vw cgr wcel cmnd crn cxp wf cv co wceq wral wrex wa w3a eqid
      wi isgrpo biimpd grpoidinv simpl ralimi reximi ismnd2 biimprcd 3exp com3l
      impcom syl mpcom exp3acom3r a1i com13 3imp syli pm2.43i ) AFGZAHGZUTUTAIZ
      VBJVBAKZBLZCLZAMZDLZAMVDVEVGAMAMNDVBOCVBOBVBOZELZVDAMVDNVEVDAMZVINCVBPQBV
      BOEVBPZRZVAUTUTVLBCDEFAVBVBSZUAUBVCVHVKUTVATZVKVHVCVNVHVCVNTTVKUTVHVCVAVF
      VENVJVENQZVIVEAMVDNVEVIAMVDNQEVBPZQZCVBOZBVBPZUTVHVCQZVATZCEBAVBVMUCVSVOC
      VBOZBVBPZUTWATVRWBBVBVQVOCVBVOVPUDUEUFVTWCUTVAVCVHWCVNTVCVHWCVNUTVAVCVHWC
      RBCDFAVBVMUGUHUIUKUJULUMUNUOUPUQURUS $.
      $( [22-Dec-2013] $) $( [2-Nov-2009] $)
  $}

