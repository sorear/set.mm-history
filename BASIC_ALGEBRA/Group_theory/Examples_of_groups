$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Examples of groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CCfld $.

  $( Extend class notation with the field of complex numbers. $)
  ccnfld $a class CCfld $.

  $( The field of complex numbers.  Other number fields and rings can be
     constructed by applying the ` |``s ` restriction operator, for instance
     ` ( CCfld |`` AA ) ` is the field of algebraic numbers.

     The contract of this set is defined entirely by ~ cnfldex , ~ cnfldadd ,
     ~ cnfldmul , and ~ cnfldbas .  We may add additional members to this in
     the future, e.g. a topology. $)
  df-cnfld $a |- CCfld = { <. ( Base ` ndx ) , CC >. ,
      <. ( +g ` ndx ) , + >. , <. ( .r ` ndx ) , x. >. } $.

  ${
    $d x y z $.
    $( The base set of the field of complex numbers.  (Contributed by Stefan
       O'Rear, 27-Nov-2014.) $)
    cnfldbas $p |- CC = ( Base ` CCfld ) $=
      ( cc cvv wcel ccnfld cbs cfv wceq cnex caddc cmul df-cnfld rngbase ax-mp
      ) ABCADEFGHAIDJBKLM $.
      $( [27-Nov-2014] $)

    $( The addition operation of the field of complex numbers.  (Contributed by
       Stefan O'Rear, 27-Nov-2014.) $)
    cnfldadd $p |- + = ( +g ` CCfld ) $=
      ( caddc cvv wcel ccnfld cplusg wceq addex cc cmul df-cnfld rngplusg ax-mp
      cfv ) ABCADEMFGHADIBJKL $.
      $( [27-Nov-2014] $)

    $( The multiplication operation of the field of complex numbers.
       (Contributed by Stefan O'Rear, 27-Nov-2014.) $)
    cnfldmul $p |- x. = ( .r ` CCfld ) $=
      ( cmul cvv wcel ccnfld cmulr cfv wceq mulex caddc df-cnfld rngmulr ax-mp
      cc ) ABCADEFGHMIDABJKL $.
      $( [27-Nov-2014] $)

    $( The field of complex numbers is a set.  (Contributed by Stefan O'Rear,
       27-Nov-2014.) $)
    cnfldex $p |- CCfld e. _V $=
      ( ccnfld cnx cbs cfv cc cop cplusg caddc cmulr cmul ctp cvv df-cnfld tpex
      eqeltri ) ABCDEFZBGDHFZBIDJFZKLMPQRNO $.
      $( [27-Nov-2014] $)

    $( The complex numbers form a commutative ring.  (Contributed by Mario
       Carneiro, 8-Jan-2015.) $)
    cncrng $p |- CCfld e. CRing $=
      ( vx vy vz ccnfld wcel wtru cc caddc cmul c1 cfv cnfldbas a1i cnfldadd cv
      wceq cc0 co 3adant1 adantl ccrg cbs cplusg cmulr cnfldmul cgrp cneg addcl
      addass 0cn addid2 negcl addcom mpancom negid eqtrd isgrpi mulcl w3a adddi
      mulass adddir ax-1cn mulid2 mulid1 mulcom iscrngd trud ) DUAEFABCGHDIJGDU
      BKPFLMHDUCKPFNMIDUDKPFUEMDUFEFABCGHDAOZUGZQLNVIBOZUHVIVKCOZUIUJVIUKVIULZV
      IGEZVJVIHRZVIVJHRZQVJGEVNVOVPPVMVJVIUMUNVIUOUPUQMVNVKGEZVIVKIRZGEFVIVKURS
      VNVQVLGEUSZVRVLIRVIVKVLIRZIRPFVIVKVLVATVSVIVKVLHRIRVRVIVLIRZHRPFVIVKVLUTT
      VSVIVKHRVLIRWAVTHRPFVIVKVLVBTJGEFVCMVNJVIIRVIPFVIVDTVNVIJIRVIPFVIVETVNVQV
      RVKVIIRPFVIVKVFSVGVH $.
      $( [8-Jan-2015] $)

    $( The complex numbers form a ring.  (Contributed by Stefan O'Rear,
       27-Nov-2014.) $)
    cnrng $p |- CCfld e. Ring $=
      ( ccnfld ccrg wcel crg cncrng crngrng ax-mp ) ABCADCEAFG $.
      $( [27-Nov-2014] $)

    $( The zero element of the field of complex numbers.  (Contributed by
       Stefan O'Rear, 27-Nov-2014.) $)
    cnfld0 $p |- 0 = ( 0g ` CCfld ) $=
      ( ccnfld c0g cfv cc0 caddc co wceq 00id cgrp cc wb crg cnrng rnggrp ax-mp
      wcel 0cn cnfldbas cnfldadd eqid grpid mp2an mpbi eqcomi ) ABCZDDDEFDGZUED
      GZHAIPZDJPUFUGKALPUHMANOQJEAUEDRSUETUAUBUCUD $.
      $( [27-Nov-2014] $)

    $( The unit element of the field of complex numbers.  (Contributed by
       Stefan O'Rear, 27-Nov-2014.) $)
    cnfld1 $p |- 1 = ( 1r ` CCfld ) $=
      ( vx ccnfld cur cfv c1 cc wcel cv cmul wceq wral ax-1cn mulid2 mulid1 jca
      co wa rgen pm3.2i crg wb cnrng cnfldbas cnfldmul eqid isrngid mpbi eqcomi
      ax-mp ) BCDZEEFGZEAHZIPULJZULEIPULJZQZAFKZQZUJEJZUKUPLUOAFULFGUMUNULMULNO
      RSBTGUQURUAUBAFBIUJEUCUDUJUEUFUIUGUH $.
      $( [27-Nov-2014] $)

    $( The additive inverse in the field of complex numbers.  (Contributed by
       Stefan O'Rear, 27-Nov-2014.) $)
    cnfldneg $p |- ( X e. CC -> ( ( invg ` CCfld ) ` X ) = -u X ) $=
      ( cc wcel ccnfld cminusg cfv cneg wceq caddc co negid wb negcl cgrp cnrng
      cc0 crg rnggrp ax-mp cnfldbas cnfldadd cnfld0 eqid grpinvid1 mp3an1 mpdan
      mpbird ) ABCZADEFZFAGZHZAUJIJPHZAKUHUJBCZUKULLZAMDNCZUHUMUNDQCUOODRSBIDUI
      PAUJTUAUBUIUCUDUEUFUG $.
      $( [27-Nov-2014] $)

    $( The complex numbers form a division ring.  (Contributed by Stefan
       O'Rear, 27-Nov-2014.) $)
    cndrng $p |- CCfld e. DivRing $=
      ( vx vy ccnfld cdr wcel wtru cc cmul c1 cv cdiv co cc0 cbs cfv a1i wne wa
      wceq adantl cnfldbas cmulr c0g cnfld0 cur cnfld1 crg cnrng mulne0 3adant1
      cnfldmul ax-1ne0 reccl recne0 recid2 isdrngd trud ) CDEFABGCHIIAJZKLZMGCN
      OSFUAPHCUBOSFUKPMCUCOSFUDPICUEOSFUFPCUGEFUHPURGEURMQRZBJZGEVAMQRURVAHLMQF
      URVAUIUJIMQFULPUTUSGEFURUMTUTUSMQFURUNTUTUSURHLISFURUOTUPUQ $.
      $( [27-Nov-2014] $)

    $( The division operation in the field of complex numbers.  (Contributed by
       Stefan O'Rear, 27-Nov-2014.)  (Revised by Mario Carneiro,
       2-Dec-2014.) $)
    cnflddiv $p |- / = ( /r ` CCfld ) $=
      ( vx vy vz cc cc0 cv cmul co wceq cmpt2 ccnfld cdiv wcel wa syl3anc cnrng
      cfv cnfldbas eqid cnfldmul csn cdif crio cinvr cdvr wne simpl simpr sylib
      eldifsn simpld simprd divval crg cui cndrng cnfld0 isdrng ax-mp rngdvcan1
      cdr simprbi eqcomi mp3an1 oveq1d rngdvcl divcan4 rngdvval eqtrd mpt2eq3ia
      eqtr3d df-div rngdvfval 3eqtr4i ) ABDDEUAUBZBFZCFGHAFZICDUCZJABDVOVQVPKUD
      QZQGHZJZLKUEQZABDVOVRVTVQDMZVPVOMZNZVRVQVPWBHZVTWEVQVPLHZVRWFWEWCVPDMZVPE
      UFZWGVRIWCWDUGWEWHWIWEWDWHWINWCWDUHVPDEUJUIZUKZWEWHWIWJULZCVQVPUMOWEWFVPG
      HZVPLHZWGWFWEWMVQVPLKUNMZWCWDWMVQIPDWBKGVOVQVPRKUOQZVOKVAMZWPVOIZUPWQWOWR
      DKWPERWPSUQURVBUSVCZWBSZTUTVDVEWEWFDMZWHWIWNWFIWOWCWDXAPDWBKVOVQVPRWSWTVF
      VDWKWLWFVPVGOVKVKDWBKGVOVSVQVPRTWSVSSZWTVHVIVJABCVLWOWBWAIPABDWBKGVOVSUNR
      TWSXBWTVMUSVN $.
      $( [2-Dec-2014] $) $( [27-Nov-2014] $)

    $( The multiplicative inverse in the field of complex numbers.
       (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    cnfldinv $p |- ( ( X e. CC /\ X =/= 0 ) ->
      ( ( invr ` CCfld ) ` X ) = ( 1 / X ) ) $=
      ( cc wcel cc0 wne wa csn cdif ccnfld cinvr cfv c1 cdiv wceq eldifsn cnrng
      co crg cnfldbas cnfld0 cndrng drngui cnflddiv cnfld1 eqid rnginvdv sylbir
      mpan ) ABCADEFABDGHZCZAIJKZKLAMQNZABDOIRCUJULPBMIUIUKLASBIDSTUAUBUCUDUKUE
      UFUHUG $.
      $( [4-Dec-2014] $)
  $}

  ${
    $d x y A $.
    cnsubglem.1 $e |- ( x e. A -> x e. CC ) $.
    cnsubglem.2 $e |- ( ( x e. A /\ y e. A ) -> ( x + y ) e. A ) $.
    cnsubglem.3 $e |- ( x e. A -> -u x e. A ) $.
    ${
      cnsubglem.4 $e |- B e. A $.
      $( Lemma for ~ resubdrg and friends. $)
      cnsubglem $p |- A e. ( SubGrp ` CCfld ) $=
        ( ccnfld csubg cfv wcel cc wss c0 wne cv caddc wral ax-mp co cminusg wa
        ssriv ne0i ralrimiva cneg wceq cnfldneg syl eqeltrd jca rgen w3a wb crg
        cgrp cnrng rnggrp cnfldbas cnfldadd eqid issubg2 mpbir3an ) CIJKLZCMNZC
        OPZAQZBQRUACLZBCSZVHIUBKZKZCLZUCZACSZACMEUDDCLVGHCDUETVNACVHCLZVJVMVPVI
        BCFUFVPVLVHUGZCVPVHMLVLVQUHEVHUIUJGUKULUMIUQLZVEVFVGVOUNUOIUPLVRURIUSTA
        BMRCIVKUTVAVKVBVCTVD $.
        $( [4-Dec-2014] $)
    $}

    cnsubrglem.4 $e |- 1 e. A $.
    cnsubrglem.5 $e |- ( ( x e. A /\ y e. A ) -> ( x x. y ) e. A ) $.
    $( Lemma for ~ resubdrg and friends. $)
    cnsubrglem $p |- A e. ( SubRing ` CCfld ) $=
      ( ccnfld csubrg cfv wcel csubg c1 cv cmul co wral cnsubglem rgen2 crg w3a
      wb cnrng cc cnfldbas cnfld1 cnfldmul issubrg2 ax-mp mpbir3an ) CIJKLZCIMK
      LZNCLZAOBOPQCLZBCRACRZABCNDEFGSGUOABCCHTIUALULUMUNUPUBUCUDABCUEIPNUFUGUHU
      IUJUK $.
      $( [4-Dec-2014] $)

    cnsubrglem.6 $e |- ( ( x e. A /\ x =/= 0 ) -> ( 1 / x ) e. A ) $.
    $( Lemma for ~ resubdrg and friends. $)
    cnsubdrglem $p |-
        ( A e. ( SubRing ` CCfld ) /\ ( CCfld |`s A ) e. DivRing ) $=
      ( ccnfld cfv wcel co cdr cc0 cdif c1 cc wceq eqid csubrg cress cnsubrglem
      cv cinvr csn wral cdiv crg cnrng wss ssriv ssdif ax-mp sseli cnfldbas cui
      cndrng cnfld0 isdrng mpbi simpri eqcomi cnflddiv idi rnginvdv sylancr wne
      wa cnfld1 eldifsn sylbi eqeltrd rgen wb issubdrg mp2an mpbir pm3.2i ) CJU
      AKLZJCUBMZNLZABCDEFGHUCZWBAUDZJUEKZKZCLZACOUFZPZUGZWGAWIWDWILZWFQWDUHMZCW
      KJUILZWDRWHPZLWFWLSUJWIWNWDCRUKWIWNUKACRDULCRWHUMUNUORUHJWNWEQWDUPJUQKZWN
      WMWOWNSZJNLZWMWPVIURRJWOOUPWOTUSUTVAVBVCVDVJWEWESWETZVEVFVGWKWDCLWDOVHVIW
      LCLWDCOVKIVLVMVNWQVTWBWJVOURWCACJWAWEOWATUSWRVPVQVRVS $.
      $( [4-Dec-2014] $)
  $}

  ${
    $d x y $.
    $( The real numbers form a division subring of the complexes.  (Contributed
       by Mario Carneiro, 4-Dec-2014.) $)
    resubdrg $p |-
        ( RR e. ( SubRing ` CCfld ) /\ ( CCfld |`s RR ) e. DivRing ) $=
      ( vx vy cr cv recn readdcl renegcl 1re remulcl rereccl cnsubdrglem ) ABCA
      DZELBDZFLGHLMILJK $.
      $( [4-Dec-2014] $)

    $( The rational numbers form a division subring of the complexes.
       (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    qsubdrg $p |-
        ( QQ e. ( SubRing ` CCfld ) /\ ( CCfld |`s QQ ) e. DivRing ) $=
      ( vx vy cq cv qcn qaddcl qnegcl cz c1 1z sselii qmulcl qreccl cnsubdrglem
      zssq ) ABCADZEPBDZFPGHCIOJKPQLPMN $.
      $( [4-Dec-2014] $)

    $( The integers form a subring of the complexes.  (Contributed by Mario
       Carneiro, 4-Dec-2014.) $)
    zsubrg $p |- ZZ e. ( SubRing ` CCfld ) $=
      ( vx vy cz cv zcn zaddcl znegcl 1z zmulcl cnsubrglem ) ABCADZEKBDZFKGHKLI
      J $.
      $( [4-Dec-2014] $)

    $( The gaussian integers form a subring of the complexes.  (Contributed by
       Mario Carneiro, 4-Dec-2014.) $)
    gzsubrg $p |- Z[i] e. ( SubRing ` CCfld ) $=
      ( vx vy cgz cv gzcn gzaddcl gznegcl c1 cz 1z zgz ax-mp gzmulcl cnsubrglem
      wcel ) ABCADZEPBDZFPGHIOHCOJHKLPQMN $.
      $( [4-Dec-2014] $)

    $( The regular absolute value function on the complex numbers is in fact an
       absolute value under our definition.  (Contributed by Mario Carneiro,
       4-Dec-2014.) $)
    absabv $p |- abs e. ( AbsVal ` CCfld ) $=
      ( vx vy cabs ccnfld cfv wcel wtru caddc cmul cc0 wceq a1i wne wbr 3adant1
      cc cv wa co ad2ant2r cabv cbs cnfldbas cplusg cnfldadd cmulr cnfldmul c0g
      eqid cnfld0 crg cnrng cr wf absf abs0 clt absgt0 biimpa absmul cle abstri
      isabvd trud ) CDUAEZFGABVEPHDICJVEVEKGVEUILPDUBEKGUCLHDUDEKGUELIDUFEKGUGL
      JDUHEKGUJLDUKFGULLPUMCUNGUOLJCEJKGUPLAQZPFZVFJMZJVFCEZUQNZGVGVHVJVFURUSOV
      GVHRZBQZPFZVLJMZRZVFVLISCEVIVLCEZISKZGVGVMVQVHVNVFVLUTTOVKVOVFVLHSCEVIVPH
      SVANZGVGVMVRVHVNVFVLVBTOVCVD $.
      $( [4-Dec-2014] $)
  $}

  ${
    gzrng.1 $e |- Z = ( CCfld |`s Z[i] ) $.
    $( Lemma for ~ gzrngunit . $)
    gzrngunitlem $p |- ( A e. ( Unit ` Z ) -> 1 <_ ( abs ` A ) ) $=
      ( cfv wcel c1 cle wbr c2 cexp co cc0 wn cgz ccnfld wceq gzsubrg ax-mp syl
      wb cui cabs sq1 cn wne ax-1ne0 csubrg crg subrgrng csubg subrgsubg cnfld0
      eqid c0g subg0 cur cnfld1 subrg1 0unit mp2b necon3bbii mpbir cn0 subrgbas
      wo cbs unitcl gzabssqcl elnn0 sylib cc gzcn abscl recnd sqeq0 abs00 eleq1
      ord cr biimpcd sylbid syld mt3i nnge1 syl5eqbr absge0 1re 0re lt01 ltleii
      wa le2sq mpanl12 syl2anc mpbird ) ABUADZEZFAUBDZGHZFIJKZWRIJKZGHZWQWTFXAG
      UCWQXAUDEZFXAGHWQXCLWPEZXDMFLUEUFXDFLNOUGDEZBUHEXDFLPTQNOBCUIBWPFLWPUMZNO
      UJDEZLBUNDPXEXGQNOUKRNOBLCULUORXEFBUPDPQNOBFCUQURRUSUTVAVBWQXCMXALPZXDWQX
      CXHWQXAVCEZXCXHVEWQANEZXINBWPAXENBVFDPQNOBCVDRXFVGZAVHSXAVIVJVRWQXHWRLPZX
      DWQWRVKEXHXLTWQWRWQAVKEZWRVSEZWQXJXMXKAVLSZAVMSZVNWRVOSWQXLALPZXDWQXMXLXQ
      TXOAVPSXQWQXDALWPVQVTWAWAWBWCXAWDSWEWQXNLWRGHZWSXBTZXPWQXMXRXOAWFSFVSELFG
      HXNXRWKXSWGLFWHWGWIWJFWRWLWMWNWO $.
      $( [4-Dec-2014] $)

    $( The units on ` Z [ i ] ` are the gaussian integers with norm ` 1 ` .
       (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    gzrngunit $p |- ( A e. ( Unit ` Z ) <->
      ( A e. Z[i] /\ ( abs ` A ) = 1 ) ) $=
      ( cfv wcel cgz cabs c1 wceq ccnfld gzsubrg cle wbr cdiv co cc cc0 syl a1i
      1re cui wa csubrg cbs subrgbas ax-mp eqid unitcl cinvr subrginv mpan gzcn
      wne clt abscl 3syl 0re lt01 gzrngunitlem ltletrd gt0ne0 syl2anc necon3bid
      cr wb abs00 cnfldinv eqtr3d crg subrgrng unitinvcl eqeltrrd ax-1cn absdiv
      mpbid syl3anc breqtrd ax-1ne0 dividi ltleii eqcomi oveq1i 3brtr4g mpanr12
      absid mp2an lerec mpbird letri3 sylancl mpbir2and jca cdif adantr eqnetrd
      csn simpr fveq2 abs0 syl6eq necon3i eldifsn sylanbrc ccj cmul c2 absvalsq
      simpl cexp oveq1d sq1 cjcl divcan3 3eqtr2d gzcjcl eqeltrd cnfldbas cnfld0
      w3a cndrng drngui subrgunit syl3anbrc impbii ) ABUADZEZAFEZAGDZHIZUBZYFYG
      YIFBYEAFJUCDEZFBUDDIKFJBCUEUFYEUGZUHZYFYIYHHLMZHYHLMZYFYNHHNOZHYHNOZLMZYF
      HHGDZYHNOZYPYQLYFHHANOZGDZYTLYFUUAYEEHUUBLMYFABUIDZDZUUAYEYFAJUIDZDZUUDUU
      AYKYFUUFUUDIKFJBYEUUEUUCACUUEUGZYLUUCUGZUJUKYFAPEZAQUMZUUFUUAIZYFYGUUIYMA
      ULZRZYFYHQUMZUUJYFYHVDEZQYHUNMZUUNYFYGUUIUUOYMUULAUOUPZYFQHYHQVDEYFUQSHVD
      EZYFTSUUQQHUNMZYFURSABCUSZUTZYHVAVBYFYHQAQYFUUIYHQIAQIZVEUUMAVFRVCVOZAVGZ
      VBVHBVIEZYFUUDYEEYKUVEKFJBCVJUFBYEUUCAYLUUHVKUKVLUUABCUSRYFHPEZUUIUUJUUBY
      TIUVFYFVMSUUMUVCHAVNVPVQHVMVRVSHYSYHNYSHUURQHLMYSHITQHUQTURVTHWEWFWAWBWCY
      FUUOUUPYNYRVEZUUQUVAUUOUUPUBUURUUSUVGTURYHHWGWDVBWHUUTYFUUOUURYIYNYOUBVEU
      UQTYHHWIWJWKWLYJAPQWPWMZEZYGUUFFEZYFYJUUIUUJUVIYGUUIYIUULWNZYJUUNUUJYJYHH
      QYGYIWQZHQUMYJVRSWOAQYHQUVBYHQGDQAQGWRWSWTXARZAPQXBXCYGYIXHYJUUFAXDDZFYJU
      UFUUAAUVNXEOZANOZUVNYJUUIUUJUUKUVKUVMUVDVBYJUVOHANYJYHXFXIOZUVOHYJUUIUVQU
      VOIUVKAXGRYJUVQHXFXIOHYJYHHXFXIUVLXJXKWTVHXJYJUVNPEZUUIUUJUVPUVNIYJUUIUVR
      UVKAXLRUVKUVMUVNAXMVPXNYGUVNFEYIAXOWNXPYKYFUVIYGUVJXSVEKFJBUVHUUEYEACPJQX
      QXRXTYAYLUUGYBUFYCYD $.
      $( [4-Dec-2014] $)
  $}

  ${
    zrng.1 $e |- Z = ( CCfld |`s ZZ ) $.
    $( The units of ` ZZ ` are the integers with norm ` 1 ` , i.e. ` 1 ` and
       ` -u 1 ` .  (Contributed by Mario Carneiro, 5-Dec-2014.) $)
    zrngunit $p |- ( A e. ( Unit ` Z ) <-> ( A e. ZZ /\ ( abs ` A ) = 1 ) ) $=
      ( cfv wcel cz c1 wceq ccnfld zsubrg ax-mp eqid cgz cress co syl cc cc0 c2
      cexp vx cui cabs wa csubrg cbs subrgbas unitcl wss ssriv gzsubrg subsubrg
      cv zgz mpbir2an cin sseqin2 oveq2i ressress mp2an 3eqtr4ri subrguss sseli
      wb mpbi gzrngunit simprbi jca csn cdif cinvr wne zcn adantr simpr ax-1ne0
      a1i eqnetrd fveq2 abs0 syl6eq necon3i eldifsn sylanbrc simpl cdiv syl2anc
      cnfldinv cr zre absresq oveq1d sq1 sqval 3eqtr3rd ax-1cn divmul syl112anc
      cmul mpbird eqeltrd w3a cnfldbas cnfld0 cndrng drngui subrgunit syl3anbrc
      eqtrd impbii ) ABUBDZEZAFEZAUCDZGHZUDZXLXMXOFBXKAFIUEDZEZFBUFDHJFIBCUGKXK
      LZUHXLAIMNOZUBDZEZXOXKYAAFXTUEDEZXKYAUIYCXRFMUIZJUAFMUAUMUNUJZMXQEZYCXRYD
      UDVDUKMFIXTXTLZULKUOFXTBYAXKIMFUPZNOZIFNOXTFNOZBYHFINYDYHFHYEFMUQVEURYFXR
      YJYIHUKJMFIXQXQUSUTCVAYALXSVBKVCYBAMEXOAXTYGVFVGPVHXPAQRVIVJZEZXMAIVKDZDZ
      FEZXLXPAQEZARVLZYLXMYPXOAVMVNZXPXNRVLYQXPXNGRXMXOVOZGRVLXPVPVQVRARXNRARHX
      NRUCDRARUCVSVTWAWBPZAQRWCWDXMXOWEZXPYNAFXPYNGAWFOZAXPYPYQYNUUBHYRYTAWHWGX
      PUUBAHZAAWSOZGHZXPXNSTOZASTOZGUUDXPAWIEZUUFUUGHXMUUHXOAWJVNAWKPXPUUFGSTOG
      XPXNGSTYSWLWMWAXPYPUUGUUDHYRAWNPWOXPGQEZYPYPYQUUCUUEVDUUIXPWPVQYRYRYTGAAW
      QWRWTXIUUAXAXRXLYLXMYOXBVDJFIBYKYMXKACQIRXCXDXEXFXSYMLXGKXHXJ $.
      $( [5-Dec-2014] $)
  $}

  ${
    $d f k n x A $.  $d f n x B $.  $d f k n x ph $.
    gsumfsum.1 $e |- ( ph -> A e. Fin ) $.
    gsumfsum.2 $e |- ( ( ph /\ k e. A ) -> B e. CC ) $.
    $( Relate a group sum on ` CCfld ` to a finite sum on the complexes.
       (Contributed by Mario Carneiro, 28-Dec-2014.) $)
    gsumfsum $p |- ( ph -> ( CCfld gsum ( k e. A |-> B ) ) = sum_ k e. A B ) $=
      ( vf vx c0 wceq cfv wcel co cv wa ccnfld cgsu syl cc0 cc vn chash cn wf1o
      c1 cfz wex wo cmpt csu cfn fz1f1o wi cnfld0 gsum0 sum0 eqtr4i mpteq1 mpt0
      syl6eq oveq2d 3eqtr4a a1i caddc ccom cseq ccnv cvv csn cdif cima cnfldbas
      sumeq1 cnfldadd ccmn crg cnrng rngcmn ax-mp adantr wf fmptd simprl simprr
      eqid wf1 f1of1 crn cdm cnvimass fdm syl5sseq wfo f1ofo forn 3syl sseqtr4d
      gsumval3 sumfc fveq2 ffvelrn sylan f1of fvco3 fsum syl5eqr eqtr4d exlimdv
      expr expimpd jaod mpd ) ABIJZBUBKZUCLZUEXNUFMZBGNZUDZGUGZOZUHZPDBCUIZQMZB
      CDUJZJZABUKLZYAEBGULRAXMYEXTXMYEUMAXMPIQMZICDUJZYCYDYGSYHPSUNUOCDUPUQXMYB
      IPQXMYBDICUIIDBICURDCUSUTVABICDVMVBVCAXOXSYEAXOOXRYEGAXOXRYEAXOXROZOZYCXN
      VDYBXQVEZUEVFKZYDYJBTVDYBPXQXNUKYKVGVHSVIVJZVKZSVLUNVNPVOLZYJPVPLYOVQPVRV
      SVCAYFYIEVTABTYBWAZYIADBCTYBFYBWEWBVTZAXOXRWCZYJXRXPBXQWFAXOXRWDZXPBXQWGR
      YJYBVGYMVKZBXQWHZYJYBWIZYTBYBYMWJYJYPUUBBJYQBTYBWKRWLYJXRXPBXQWMUUABJYSXP
      BXQWNXPBXQWOWPWQYNWEWRYJYDBHNZYBKZHUJYLBCHDWSYJBUUDUANZXQKZYBKZHUAXQYKXNU
      UCUUFYBWTYRYSYJYPUUCBLUUDTLYQBTUUCYBXAXBYJXPBXQWAZUUEXPLUUEYKKUUGJYJXRUUH
      YSXPBXQXCRXPBUUEYBXQXDXBXEXFXGXIXHXJXKXL $.
      $( [28-Dec-2014] $)
  $}

