$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Definition and basic properties of Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c AbelOp $.

  $( Extend class notation with the class of all Abelian group operations. $)
  cablo $a class AbelOp $.

  ${
    $d g x y $.
    $( Define the class of all Abelian group operations. $)
    df-ablo $a |- AbelOp
     = { g e. GrpOp | A. x e. ran g A. y e. ran g ( x g y ) = ( y g x ) } $.
  $}

  ${
    $d g x y G $.  $d g x y X $.
    isabl.1 $e |- X = ran G $.
    $( The predicate "is an Abelian (commutative) group operation." $)
    isablo $p |- ( G e. AbelOp <-> ( G e. GrpOp /\
             A. x e. X A. y e. X ( x G y ) = ( y G x ) ) ) $=
      ( vg cv co wceq crn wral cgr cablo rneq syl6eqr raleq raleqbi1dv syl oveq
      wb eqeq12d 2ralbidv bitrd df-ablo elrab2 ) AGZBGZFGZHZUGUFUHHZIZBUHJZKZAU
      LKZUFUGCHZUGUFCHZIZBDKADKZFCLMUHCIZUNUKBDKZADKZURUSULDIUNVATUSULCJDUHCNEO
      UMUTAULDUKBULDPQRUSUKUQABDDUSUIUOUJUPUFUGUHCSUGUFUHCSUAUBUCABFUDUE $.
      $( [2-Nov-2006] $)
  $}

  ${
    $d x y G $.
    $( An Abelian group operation is a group operation. $)
    ablogrpo $p |- ( G e. AbelOp -> G e. GrpOp ) $=
      ( vx vy cablo wcel cgr cv co wceq crn wral eqid isablo simplbi ) ADEAFEBG
      ZCGZAHPOAHICAJZKBQKBCAQQLMN $.
      $( [2-Nov-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y G $.  $d x y X $.
    ablcom.1 $e |- X = ran G $.
    $( An Abelian group operation is commutative. $)
    ablocom $p |- ( ( G e. AbelOp /\ A e. X /\ B e. X ) ->
                 ( A G B ) = ( B G A ) ) $=
      ( vx vy cablo wcel co wceq cv wral cgr isablo simprbi oveq1 oveq2 eqeq12d
      wa rcla42v syl5com 3impib ) CHIZADIZBDIZABCJZBACJZKZUDFLZGLZCJZUKUJCJZKZG
      DMFDMZUEUFTUIUDCNIUOFGCDEOPUNUIAUKCJZUKACJZKFGABDDUJAKULUPUMUQUJAUKCQUJAU
      KCRSUKBKUPUGUQUHUKBACRUKBACQSUAUBUC $.
      $( [2-Nov-2006] $)

    $( Commutative/associative law for Abelian groups. $)
    ablo32 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cablo wcel w3a wa co wceq ablocom 3adant3r1 oveq2d cgr ablogrpo grpoass
      sylan 3ancomb sylan2b 3eqtr4d ) DGHZAEHZBEHZCEHZIZJZABCDKZDKZACBDKZDKZABD
      KCDKZACDKBDKZUHUIUKADUCUEUFUIUKLUDBCDEFMNOUCDPHZUGUMUJLDQZABCDEFRSUCUOUGU
      NULLZUPUGUOUDUFUEIUQUDUEUFTACBDEFRUASUB $.
      $( [26-Apr-2007] $)

    $( Commutative/associative law for Abelian groups. $)
    ablo4 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X ) /\ ( C e. X /\
   D e. X ) ) -> ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( wcel wa wceq w3a simprlr simprrl 3jca syldan grpocl 3expb grpoass sylan
      co cablo simprll ablo32 oveq1d ablogrpo adantrr simprrr adantrlr adantrrr
      cgr 3eqtr3d 3impb ) EUAHZAFHZBFHZIZCFHZDFHZIZABETZCDETETZACETZBDETETZJUMU
      PUSIZIZUTCETZDETZVBBETZDETZVAVCVEVFVHDEUMVDUNUOUQKVFVHJVEUNUOUQUMUNUOUSUB
      UMUNUOUSLUMUPUQURMNABCEFGUCOUDUMEUJHZVDVGVAJZEUEZVJVDUTFHZUQURKVKVJVDIZVM
      UQURVJUPVMUSVJUNUOVMABEFGPQUFVJUPUQURMVJUPUQURUGZNUTCDEFGROSUMVJVDVIVCJZV
      LVJVDVBFHZUOURKVPVNVQUOURVJUPUQVQURVJUNUQVQUOVJUNUQVQACEFGPQUHUIVJUNUOUSL
      VONVBBDEFGROSUKUL $.
      $( [26-Apr-2007] $)
  $}

  ${
    $d x y G $.  $d x y X $.
    isabli.1 $e |- G e. GrpOp $.
    isabli.2 $e |- dom G = ( X X. X ) $.
    isabli.3 $e |- ( ( x e. X /\ y e. X ) -> ( x G y ) = ( y G x ) ) $.
    $( Properties that determine an Abelian group operation. $)
    isabloi $p |- G e. AbelOp $=
      ( cablo wcel cgr cv co wceq wral grporn isablo rgen2a mpbir2an ) CHICJIAK
      ZBKZCLTSCLMZBDNADNABCDCDEFOPEUAABDGQR $.
      $( [9-Apr-2008] $) $( [5-Nov-2006] $)
  $}

  ${
    abldiv.1 $e |- X = ran G $.
    abldiv.3 $e |- D = ( /g ` G ) $.
    $( Law for group multiplication and division. $)
    ablomuldiv $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
     ( ( A G B ) D C ) = ( ( A D C ) G B ) ) $=
      ( cablo wcel w3a wa co wceq ablocom 3adant3r3 oveq1d 3ancoma cgr ablogrpo
      grpomuldivass sylan sylan2b simpr2 grpodivcl syl3an1 3adant3r2 jca syldan
      3expb 3eqtrd ) EIJZAFJZBFJZCFJZKZLZABEMZCDMBAEMZCDMZBACDMZEMZVABEMZUQURUS
      CDULUMUNURUSNUOABEFGOPQUPULUNUMUOKZUTVBNZUMUNUORULESJZVDVEETZBACDEFGHUAUB
      UCULUPUNVAFJZLVBVCNZUQUNVHULUMUNUOUDULUMUOVHUNULVFUMUOVHVGACDEFGHUEUFUGUH
      ULUNVHVIBVAEFGOUJUIUK $.
      $( [15-Feb-2008] $)

    $( Law for double group division. $)
    ablodivdiv $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
    ( A D ( B D C ) ) = ( ( A D B ) G C ) ) $=
      ( cablo wcel w3a wa co cgr wceq ablogrpo grpodivdiv 3ancomb grpomuldivass
      sylan ablomuldiv eqtr3d sylan2b eqtrd ) EIJZAFJZBFJZCFJZKZLABCDMDMZACBDME
      MZABDMCEMZUEENJZUIUJUKOEPZABCDEFGHQTUIUEUFUHUGKZUKULOUFUGUHRUEUOLACEMBDMZ
      UKULUEUMUOUPUKOUNACBDEFGHSTACBDEFGHUAUBUCUD $.
      $( [29-Feb-2008] $)

    $( Law for double group division. $)
    ablodivdiv4 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
    ( ( A D B ) D C ) = ( A D ( B G C ) ) ) $=
      ( cablo wcel w3a wa co cgn cfv cgr wceq ablogrpo simpl grpodivcl syl3anc
      3adant3r3 simpr3 eqid grpodivval sylan simpr1 simpr2 simp3 grpoinvcl 3jca
      syl2an ablodivdiv syldan grpodivinv syl3an1 3adant3r1 oveq2d 3eqtr2d ) EI
      JZAFJZBFJZCFJZKZLZABDMZCDMZVFCENOZOZEMZABVIDMZDMZABCEMZDMUTEPJZVDVGVJQZER
      ZVNVDLVNVFFJZVCVOVNVDSVNVAVBVQVCABDEFGHTUBVNVAVBVCUCVFCDEVHFGVHUDZHUEUAUF
      UTVDVAVBVIFJZKVLVJQVEVAVBVSUTVAVBVCUGUTVAVBVCUHUTVNVCVSVDVPVAVBVCUICEVHFG
      VRUJULUKABVIDEFGHUMUNVEVKVMADUTVBVCVKVMQZVAUTVNVBVCVTVPBCDEVHFGVRHUOUPUQU
      RUS $.
      $( [29-Feb-2008] $)

    $( Swap the second and third terms in a double division. $)
    ablodiv32 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
   ( ( A D B ) D C ) = ( ( A D C ) D B ) ) $=
      ( cablo wcel w3a wa co wceq ablocom 3adant3r1 ablodivdiv4 3ancomb sylan2b
      oveq2d 3eqtr4d ) EIJZAFJZBFJZCFJZKZLZABCEMZDMACBEMZDMZABDMCDMACDMBDMZUGUH
      UIADUBUDUEUHUINUCBCEFGOPTABCDEFGHQUFUBUCUEUDKUKUJNUCUDUERACBDEFGHQSUA $.
      $( [29-Feb-2008] $)

    $( Cancellation law for group division.  ( ~ nnncan analog.) $)
    ablonnncan $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
      ( ( A D ( B D C ) ) D C ) = ( A D B ) ) $=
      ( cablo wcel w3a wa wceq simpr1 cgr ablogrpo grpodivcl syl3an1 3adant3r1
      co simpr3 3jca ablodivdiv4 syldan grponpcan oveq2d eqtrd ) EIJZAFJZBFJZCF
      JZKZLZABCDTZDTCDTZAUNCETZDTZABDTUHULUIUNFJZUKKUOUQMUMUIURUKUHUIUJUKNUHUJU
      KURUIUHEOJZUJUKUREPZBCDEFGHQRSUHUIUJUKUAUBAUNCDEFGHUCUDUMUPBADUHUJUKUPBMZ
      UIUHUSUJUKVAUTBCDEFGHUERSUFUG $.
      $( [29-Feb-2008] $)

    $( Cancellation law for group division.  ( ~ nncan analog.) $)
    ablonncan $p |- ( ( G e. AbelOp /\ A e. X /\ B e. X ) ->
             ( A D ( A D B ) ) = B ) $=
      ( cablo wcel w3a co cgi cfv wceq wa id 3anidm12 ablodivdiv sylan2 sylan
      3impb cgr ablogrpo eqid grpodivid 3adant3 oveq1d grpolid 3adant2 3eqtrd )
      DHIZAEIZBEIZJZAABCKCKZAACKZBDKZDLMZBDKZBUKULUMUOUQNZULUMOUKULULUMJZUTULUM
      VAVAPQAABCDEFGRSUAUNUPURBDUKULUPURNZUMUKDUBIZULVBDUCZACURDEFGURUDZUETUFUG
      UKUMUSBNZULUKVCUMVFVDBURDEFVEUHTUIUJ $.
      $( [7-Mar-2008] $)

    $( Cancellation law for group division.  ( ~ nnncan1 analog.) $)
    ablonnncan1 $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
         ( ( A D B ) D ( A D C ) ) = ( C D B ) ) $=
      ( cablo wcel w3a wa wceq simpr1 simpr2 cgr ablogrpo grpodivcl 3adant3r2
      co syl3an1 3jca ablodiv32 syldan ablonncan oveq1d eqtrd ) EIJZAFJZBFJZCFJ
      ZKZLZABDTACDTZDTZAUNDTZBDTZCBDTUHULUIUJUNFJZKUOUQMUMUIUJURUHUIUJUKNUHUIUJ
      UKOUHUIUKURUJUHEPJUIUKUREQACDEFGHRUASUBABUNDEFGHUCUDUMUPCBDUHUIUKUPCMUJAC
      DEFGHUESUFUG $.
      $( [7-Mar-2008] $)
  $}

  ${
    $d A k m $.  $d B k m $.  $d G k m $.  $d K m $.  $d P k m $.  $d X k m $.
    gxdi.1 $e |- X = ran G $.
    gxdi.2 $e |- P = ( ^g ` G ) $.
    $( Distribution of group power over group operation for abelian groups.
       (Contributed by Paul Chapman, 17-Apr-2009.) $)
    gxdi $p |- ( ( G e. AbelOp /\ ( A e. X /\ B e. X ) /\ K e. ZZ ) ->
                 ( ( A G B ) P K ) = ( ( A P K ) G ( B P K ) ) ) $=
      ( wcel co wceq wi cc0 oveq2 oveq12d eqeq12d cfv 3ad2ant1 syl2anc syl3anc
      vm vk cablo wa cz cv cneg c1 caddc w3a cgi cgr ablogrpo eqid grpoidcl syl
      grpolid simp2 gx0 simp3 grpocl syl3an1 3eqtr4rd nn0z gxsuc oveq1 3ad2ant3
      cn0 simp11 gxcl ablo4 syl122anc 3eqtrd eqtr4d 3exp syl5 nnz gxneg ablocom
      cn cgn wb eqeq1 mpbird fveq2d grpoinvop zindd 3expb 3impia ) DUCIZAFIZBFI
      ZUDEUEIZABDJZECJZAECJZBECJZDJZKZWJWKWLWMWSLWNUAUFZCJZAWTCJZBWTCJZDJZKWNMC
      JZAMCJZBMCJZDJZKWNUBUFZCJZAXICJZBXICJZDJZKZWNXIUGZCJZAXOCJZBXOCJZDJZKZWNX
      IUHUIJZCJZAYACJZBYACJZDJZKZWSWJWKWLUJZUAUBEWTMKZXAXEXDXHWTMWNCNYHXBXFXCXG
      DWTMACNWTMBCNOPWTXIKZXAXJXDXMWTXIWNCNYIXBXKXCXLDWTXIACNWTXIBCNOPWTYAKZXAY
      BXDYEWTYAWNCNYJXBYCXCYDDWTYAACNWTYABCNOPWTXOKZXAXPXDXSWTXOWNCNYKXBXQXCXRD
      WTXOACNWTXOBCNOPWTEKZXAWOXDWRWTEWNCNYLXBWPXCWQDWTEACNWTEBCNOPYGDUKQZYMDJZ
      YMXHXEYGDULIZYMFIZYNYMKWJWKYOWLDUMZRZYGYOYPYRYMDFGYMUNZUOUPYMYMDFGYSUQSYG
      XFYMXGYMDYGYOWKXFYMKYRWJWKWLURZACYMDFGYSHUSSYGYOWLXGYMKYRWJWKWLUTZBCYMDFG
      YSHUSSOYGYOWNFIZXEYMKYRWJYOWKWLUUBYQABDFGVAVBZWNCYMDFGYSHUSSVCXIVHIXIUEIZ
      YGXNYFLXIVDYGUUDXNYFYGUUDXNUJZYBXKADJZXLBDJZDJZYEUUEYBXJWNDJZXMWNDJZUUHUU
      EYOUUBUUDYBUUIKYGUUDYOXNYRRZYGUUDUUBXNUUCRZYGUUDXNURZWNCDXIFGHVETXNYGUUIU
      UJKUUDXJXMWNDVFVGUUEWJXKFIZXLFIZWKWLUUJUUHKWJWKWLUUDXNVIZUUEYOWKUUDUUNUUK
      YGUUDWKXNYTRZUUMACDXIFGHVJTZUUEYOWLUUDUUOUUKYGUUDWLXNUUARZUUMBCDXIFGHVJTZ
      UUQUUSXKXLABDFGVKVLVMUUEYCUUFYDUUGDUUEYOWKUUDYCUUFKUUKUUQUUMACDXIFGHVETUU
      EYOWLUUDYDUUGKUUKUUSUUMBCDXIFGHVETOVNVOVPXIVTIUUDYGXNXTLXIVQYGUUDXNXTUUEX
      PXKDWAQZQZXLUVAQZDJZXSUUEXPXJUVAQZXLXKDJZUVAQZUVDUUEYOUUBUUDXPUVEKUUKUULU
      UMWNCDXIUVAFGUVAUNZHVRTUUEXJUVFUVAUUEXJUVFKZXMUVFKZUUEWJUUNUUOUVJUUPUURUU
      TXKXLDFGVSTXNYGUVIUVJWBUUDXJXMUVFWCVGWDWEUUEYOUUOUUNUVGUVDKUUKUUTUURXLXKD
      UVAFGUVHWFTVMUUEXQUVBXRUVCDUUEYOWKUUDXQUVBKUUKUUQUUMACDXIUVAFGUVHHVRTUUEY
      OWLUUDXRUVCKUUKUUSUUMBCDXIUVAFGUVHHVRTOVNVOVPWGWHWI $.
      $( [17-Apr-2009] $)
  $}

  ${
    $d ph u x y z $.  $d G u n x y z $.  $d X u n x y z $.  $d U u n x y z $.
    $d N n $.
    isgrpda.1 $e |- ( ph -> X e. _V ) $.
    isgrpda.2 $e |- ( ph -> G : ( X X. X ) --> X ) $.
    isgrpda.3 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                            -> ( ( x G y ) G z ) = ( x G ( y G z ) ) ) $.
    isgrpda.4 $e |- ( ph -> U e. X ) $.
    isgrpda.5 $e |- ( ( ph /\ x e. X ) -> ( U G x ) = x ) $.

    ${
      isgrpda.6 $e |- ( ( ph /\ x e. X ) -> E. n e. X ( n G x ) = U ) $.
      $( Properties that determine a group operation.  (Contributed by Jeff
         Madsen, 1-Dec-2009.) $)
      isgrpda $p |- ( ph -> G e. GrpOp ) $=
        ( vu co wceq wral wrex cvv cgr wcel crn cxp wf cv w3a wfo adantr eqcomd
        wa simpr rcla4eov syl3anc ralrimiva foov sylanbrc forn xpeq1 3syl xpeq2
        eqtrd feq2d wb feq3 bitrd raleqdv raleqbidv rexeqdv rexeqbidv 3anbi123d
        syl anbi2d ralrimivvva oveq1 eqeq1d cbvrexv jca rexbidv anbi12d ralbidv
        sylibr eqeq2 rcla4ev syl2anc mpbir3and xpexg fex eqid isgrpo mpbird ) A
        GUAUBZGUCZWMUDZWMGUEZBUFZCUFZGPDUFZGPWPWQWRGPZGPQZDWMRZCWMRZBWMRZOUFZWP
        GPZWPQZWQWPGPZXDQZCWMSZUKZBWMRZOWMSZUGZAXMHHUDZHGUEZWTDHRZCHRZBHRZXFXHC
        HSZUKZBHRZOHSZAWOXOXCXRXLYBAWOXNWMGUEZXOAWNXNWMGAWNHWMUDZXNAXNHGUHZWMHQ
        ZWNYDQAXOWPWSQDHSCHSZBHRYEJAYGBHAWPHUBZUKZEHUBZYHWPEWPGPZQYGAYJYHLUIAYH
        ULYIYKWPMUJCDHHEWPWPGUMUNUOCDBHHHGUPUQZXNHGURZWMHWMUSUTAYEYFYDXNQYLYMWM
        HHVAUTVBVCAYEYFYCXOVDYLYMWMHXNGVEUTVFAXBXQBWMHAYEYFYLYMVLZAXAXPCWMHYNAW
        TDWMHYNVGVHVHAXKYAOWMHYNAXJXTBWMHYNAXIXSXFAXHCWMHYNVIVMVHVJVKJAWTBCDHHH
        KVNAYJYKWPQZXGEQZCHSZUKZBHRZYBLAYRBHYIYOYQMYIFUFZWPGPZEQZFHSYQNYPUUBCFH
        WQYTQXGUUAEWQYTWPGVOVPVQWBVRUOYAYSOEHXDEQZXTYRBHUUCXFYOXSYQUUCXEYKWPXDE
        WPGVOVPUUCXHYPCHXDEXGWCVSVTWAWDWEWFAGTUBZWLXMVDAXOXNTUBZUUDJAHTUBZUUFUU
        EIIHHTTWGWEXNHTGWHWEBCDOTGWMWMWIWJVLWK $.
        $( [1-Dec-2009] $)
    $}

    ${
      isgrpod.6 $e |- ( ( ph /\ x e. X ) -> N e. X ) $.
      isgrpod.7 $e |- ( ( ph /\ x e. X ) -> ( N G x ) = U ) $.
      $( Properties that determine a group operation.  (Renamed from ~ isgrpd
         to ~ isgrpod to prevent naming conflict. -NM 5-Jun-2013) (Contributed
         by Jeff Madsen, 1-Dec-2009.) $)
      isgrpod $p |- ( ph -> G e. GrpOp ) $=
        ( vn cv wcel co wceq wa wrex oveq1 eqeq1d rcla4ev syl2anc isgrpda ) ABC
        DEPFHIJKLMABQZHRUAGHRGUHFSZETZPQZUHFSZETZPHUBNOUMUJPGHUKGTULUIEUKGUHFUC
        UDUEUFUG $.
        $( [1-Dec-2009] $)
    $}

    ${
      isablda.6 $e |- ( ( ph /\ x e. X ) -> E. n e. X ( n G x ) = U ) $.
      isablda.7 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                              -> ( x G y ) = ( y G x ) ) $.
      $( Properties that determine an Abelian group operation.  (Contributed by
         Jeff Madsen, 11-Jun-2010.) $)
      isabloda $p |- ( ph -> G e. AbelOp ) $=
        ( wcel cv co wceq cdm cgr crn wral cablo isgrpda wa grporndm syl cxp wf
        fdm dmeqd dmxpid syl6eq eqtrd eleq2d anbi12d ex sylbid ralrimivv isablo
        eqid sylanbrc ) AGUAPZBQZCQZGRVFVEGRSZCGUBZUCBVHUCGUDPABCDEFGHIJKLMNUEZ
        AVGBCVHVHAVEVHPZVFVHPZUFVEHPZVFHPZUFZVGAVJVLVKVMAVHHVEAVHGTZTZHAVDVHVPS
        VIGUGUHAVPHHUIZTHAVOVQAVQHGUJVOVQSJVQHGUKUHULHUMUNUOZUPAVHHVFVRUPUQAVNV
        GOURUSUTBCGVHVHVBVAVC $.
        $( [11-Jun-2010] $)
    $}

    ${
      isabld.6 $e |- ( ( ph /\ x e. X ) -> N e. X ) $.
      isabld.7 $e |- ( ( ph /\ x e. X ) -> ( N G x ) = U ) $.
      isabld.8 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                                            -> ( x G y ) = ( y G x ) ) $.
      $( Properties that determine an Abelian group operation.  (Changed label
         from ~ isabld to ~ isablod -NM 6-Aug-2013.)  (Contributed by Jeff
         Madsen, 5-Dec-2009.) $)
      isablod $p |- ( ph -> G e. AbelOp ) $=
        ( vn cv wcel wceq wa co wrex oveq1 eqeq1d rcla4ev syl2anc isabloda ) AB
        CDEQFHIJKLMABRZHSUAGHSGUIFUBZETZQRZUIFUBZETZQHUCNOUNUKQGHULGTUMUJEULGUI
        FUDUEUFUGPUH $.
        $( [5-Dec-2009] $)
    $}
  $}

