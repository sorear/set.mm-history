$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Subgroups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SubGrpOp $.

  $( Extend class notation to include the class of subgroups. $)
  csubgo $a class SubGrpOp $.

  $( Define the set of subgroups of ` g ` . $)
  df-subgo $a |- SubGrpOp = ( g e. GrpOp |-> ( GrpOp i^i ~P g ) ) $.

  ${
    $d G g h $.  $d H h $.
    $( The predicate "is a subgroup of ` G ` ."  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    issubgo $p |- ( H e. ( SubGrpOp ` G ) <->
                   ( G e. GrpOp /\ H e. GrpOp /\ H C_ G ) ) $=
      ( vg cgr wcel csubgo cfv wa wss w3a cpw cin cvv inss2 pwexg ssexg sylancr
      wceq cv pm5.32i pweq ineq2d df-subgo fvmptg mpdan eleq2d elin elpwg bitri
      syl6bb crab dmmpt ssrab2 eqsstri elfvdm sseldi pm4.71ri 3anass 3bitr4i
      cdm ) ADEZBAFGZEZHVABDEZBAIZHZHVCVAVDVEJVAVCVFVAVCBDAKZLZEZVFVAVBVHBVAVHM
      EZVBVHRVAVHVGIVGMEVJDVGNADOVHVGMPQCADCSZKZLZVHDMFVKARVLVGDVKAUAUBCUCZUDUE
      UFVIVDBVGEZHVFBDVGUGVDVOVEBADUHTUIUJTVCVAVCFUTZDAVPVMMEZCDUKDCDVMFVNULVQC
      DUMUNBAFUOUPUQVAVDVEURUS $.
      $( [12-Jul-2014] $) $( [3-Mar-2008] $)
  $}

  ${
    subgores.1 $e |- W = ran H $.
    $( A subgroup operation is the restriction of its parent group operation to
       its underlying set.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgores $p |- ( H e. ( SubGrpOp ` G ) -> H = ( G |` ( W X. W ) ) ) $=
      ( csubgo cfv wcel cxp cres wfun wss cdm wceq cgr crn issubgo simp1bi 3syl
      wfo grpofo fofun simp3bi wf simp2bi fof fdm eqimss2 fun2ssres syl3anc wfn
      eqid fofn fnresdm syl eqtr2d ) BAEFGZACCHZIZBUQIZBUPAJZBAKZUQBLZKZURUSMUP
      ANGZAOZVEHZVEASUTUPVDBNGZVAABPZQAVEVEUKTVFVEAUARUPVDVGVAVHUBUPUQCBUCZVBUQ
      MVCUPVGUQCBSZVIUPVDVGVAVHUDZBCDTZUQCBUERUQCBUFUQVBUGRUQABUHUIUPVGVJUSBMZV
      KVLVJBUQUJVMUQCBULUQBUMUNRUO $.
      $( [3-Mar-2008] $)

    $( The result of a subgroup operation is the same as the result of its
       parent operation.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgoov $p |- ( ( H e. ( SubGrpOp ` G ) /\ ( A e. W /\ B e. W ) ) ->
                   ( A H B ) = ( A G B ) ) $=
      ( csubgo cfv wcel wa co cxp cres subgores oveqd ovres sylan9eq ) DCGHIZAE
      IBEIJABDKABCEELMZKABCKRDSABCDEFNOABEECPQ $.
      $( [8-Jul-2014] $) $( [3-Mar-2008] $)
  $}

  ${
    subgornss.1 $e |- X = ran G $.
    subgornss.2 $e |- W = ran H $.
    $( The underlying set of a subgroup is a subset of its parent group's
       underlying set.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgornss $p |- ( H e. ( SubGrpOp ` G ) -> W C_ X ) $=
      ( csubgo cfv wcel crn wss cima imassrn cres subgores rneqd df-ima syl6eqr
      cxp sseq1d mpbiri 3sstr4g ) BAGHIZBJZAJZCDUCUDUEKACCSZLZUEKAUFMUCUDUGUEUC
      UDAUFNZJUGUCBUHABCFOPAUFQRTUAFEUB $.
      $( [3-Mar-2008] $)
  $}

  ${
    subgoid.1 $e |- U = ( Id ` G ) $.
    subgoid.2 $e |- T = ( Id ` H ) $.
    $( The identity element of a subgroup is the same as its parent's.
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgoid $p |- ( H e. ( SubGrpOp ` G ) -> T = U ) $=
      ( csubgo cfv wcel wceq co crn id cgr wss issubgo simp2bi grpoidcl syl2anc
      eqid syl subgoov grpolid eqtr3d wb simp1bi subgornss sseldd grpoid mpbird
      syl12anc ) DCGHIZABJZAACKZAJZULAADKZUNAULULADLZIZURUPUNJULMULDNIZURULCNIZ
      USDCOZCDPZQZADUQUQTZFRUAZVEAACDUQVDUBUKULUSURUPAJVCVEAADUQVDFUCSUDULUTACL
      ZIUMUOUEULUTUSVAVBUFULUQVFACDUQVFVFTZVDUGVEUHABCVFVGEUISUJ $.
      $( [3-Mar-2008] $)
  $}

  ${
    subgoinv.1 $e |- W = ran H $.
    subgoinv.2 $e |- M = ( inv ` G ) $.
    subgoinv.3 $e |- N = ( inv ` H ) $.
    $( The inverse of a subgroup element is the same as its inverse in the
       parent group.  (Contributed by Mario Carneiro, 8-Jul-2014.) $)
    subgoinv $p |- ( ( H e. ( SubGrpOp ` G ) /\ A e. W ) ->
                    ( N ` A ) = ( M ` A ) ) $=
      ( csubgo cfv wcel wceq co cgi cgr wss eqid sylan adantr wa grporinv simpl
      issubgo simp2bi grpoinvcl subgoov syl12anc subgoid 3eqtr3d crn wb simp1bi
      simpr subgornss sselda sseldd grpoinvid1 syl3anc mpbird eqcomd ) CBJKLZAF
      LZUAZADKZAEKZVDVEVFMZAVFBNZBOKZMZVDAVFCNZCOKZVHVIVBCPLZVCVKVLMVBBPLZVMCBQ
      ZBCUDZUEZAVLCEFGVLRZIUBSVDVBVCVFFLZVKVHMVBVCUCVBVCUNVBVMVCVSVQACEFGIUFSZA
      VFBCFGUGUHVBVLVIMVCVLVIBCVIRZVRUITUJVDVNABUKZLVFWBLVGVJULVBVNVCVBVNVMVOVP
      UMTVBFWBABCFWBWBRZGUOZUPVDFWBVFVBFWBQVCWDTVTUQAVFVIBDWBWCWAHURUSUTVA $.
      $( [8-Jul-2014] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d G x y $.  $d H x y $.  $d Y x y $.
    issubgoilem.1 $e |- ( ( x e. Y /\ y e. Y ) -> ( x H y ) = ( x G y ) ) $.
    $( Lemma for ~ issubgoi . $)
    issubgoilem $p |- ( ( A e. Y /\ B e. Y ) -> ( A H B ) = ( A G B ) ) $=
      ( cv co wceq oveq1 eqeq12d oveq2 vtocl2ga ) AIZBIZFJZPQEJZKCQFJZCQEJZKCDF
      JZCDEJZKABCDGGPCKRTSUAPCQFLPCQELMQDKTUBUAUCQDCFNQDCENMHO $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d G a b $.  $d H a b x y $.  $d H b x y z $.  $d N a b y $.
    $d U a b x y $.  $d U b x y z $.  $d Y a b x y $.  $d Y b x y z $.
    issubgoi.1 $e |- G e. GrpOp $.
    issubgoi.2 $e |- X = ran G $.
    issubgoi.3 $e |- U = ( Id ` G ) $.
    issubgoi.4 $e |- N = ( inv ` G ) $.
    issubgoi.5 $e |- Y C_ X $.
    issubgoi.6 $e |- H = ( G |` ( Y X. Y ) ) $.
    issubgoi.7 $e |- ( ( x e. Y /\ y e. Y ) -> ( x G y ) e. Y ) $.
    issubgoi.8 $e |- U e. Y $.
    issubgoi.9 $e |- ( x e. Y -> ( N ` x ) e. Y ) $.
    $( Properties that determine a subgroup.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    issubgoi $p |- H e. ( SubGrpOp ` G ) $=
      ( wcel co wceq vz va vb csubgo cfv cgr wss cv crn cvv rnexg ax-mp eqeltri
      ssexi cxp wf wfn wral cres wfo grpofo fof xpss12 mp2an fssres feq1i mpbir
      mp2b ffn oveqi ovres syl5eq issubgoilem eqeltrd rgen2a ffnov mpbir2an w3a
      wa sseli grpoass mpan syl3an sylan 3impa oveq1d 3adant3 eqtrd fovcl 3impb
      3adant1 3eqtr4d grpolid sylancr mpancom grpolinv isgrpoi eqsstri mpbir3an
      sylan2 oveq2d resss issubgo ) EDUDUERDUFRZEUFREDUGIABUACEAUHZFUEZHHGGDUIZ
      UJJXDXGUJRIDUFUKULUMMUNHHUOZHEUPEXHUQZXEBUHZESZHRZBHURAHURXHGEUPZXIXMXHGD
      XHUSZUPZGGUOZGDUPZXHXPUGZXOXDXPGDUTXQIDGJVAXPGDVBVHHGUGZXSXRMMHGHGVCVDXPG
      XHDVEVDXHGEXNNVFVGXHGEVIULXLABHXEHRZXJHRZVSZXKXEXJDSZHUBUCXEXJDEHUBUHZHRU
      CUHZHRVSYDYEESYDYEXNSYDYEDSEXNYDYENVJYDYEHHDVKVLZVMZOVNZVOABHHHEVPVQZXTYA
      UAUHZHRZVRZYCYJDSZXEXJYJDSZDSZXKYJESZXEXJYJESZESZXTXEGRZYAXJGRZYKYJGRZYMY
      OTZHGXEMVTZHGXJMVTHGYJMVTXDYSYTUUAVRUUBIXEXJYJDGJWAWBWCYLYPXKYJDSZYMXTYAY
      KYPUUDTZYBXLYKUUEYHUBUCXKYJDEHYFVMWDWEXTYAUUDYMTYKYBXKYCYJDYGWFWGWHYLYRXE
      YQDSZYOXTYAYKYRUUFTZYAYKVSZXTYQHRUUGXJYJHHHEYIWIUBUCXEYQDEHYFVMWTWJYAYKUU
      FYOTXTUUHYQYNXEDUBUCXJYJDEHYFVMXAWKWHWLPXTCXEESZCXEDSZXECHRXTUUIUUJTPUBUC
      CXEDEHYFVMWBXTXDYSUUJXETIUUCXECDGJKWMWNWHQXTXFXEESZXFXEDSZCXFHRXTUUKUULTQ
      UBUCXFXEDEHYFVMWOXTXDYSUULCTIUUCXECDFGJKLWPWNWHWQEXNDNDXHXBWRDEXCWS $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d G x y $.  $d H x y $.
    $( A subgroup of an Abelian group is Abelian.  (Contributed by Paul
       Chapman, 25-Apr-2008.) $)
    subgoablo $p |- ( ( G e. AbelOp /\ H e. ( SubGrpOp ` G ) )
           -> H e. AbelOp ) $=
      ( vx vy cablo wcel csubgo cfv wa cv co wceq crn wral sseld isablo subgoov
      eqid cgr adantll simpr subgornss anim12d simprbi ra42 syl sylan9r ancom2s
      wi imp 3eqtr4d ralrimivva wss issubgo simp2bi biimpri sylan syl2anc ) AEF
      ZBAGHFZIZUTCJZDJZBKZVCVBBKZLZDBMZNCVGNZBEFZUSUTUAVAVFCDVGVGVAVBVGFZVCVGFZ
      IZIVBVCAKZVCVBAKZVDVEVAVLVMVNLZUTVLVBAMZFZVCVPFZIZUSVOUTVJVQVKVRUTVGVPVBA
      BVGVPVPRZVGRZUBZOUTVGVPVCWBOUCUSVODVPNCVPNZVSVOUIUSASFZWCCDAVPVTPUDVOCDVP
      VPUEUFUGUJUTVLVDVMLUSVBVCABVGWAQTUTVLVEVNLZUSUTVKVJWEVCVBABVGWAQUHTUKULUT
      BSFZVHVIUTWDWFBAUMABUNUOVIWFVHICDBVGWAPUPUQUR $.
      $( [25-Apr-2008] $)
  $}

