$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Subgroups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SubGrpOp $.

  $( Extend class notation to include the class of subgroups. $)
  csubg $a class SubGrpOp $.

  $( Define the set of subgroups of ` g ` . $)
  df-subg $a |- SubGrpOp = ( g e. GrpOp |-> ( GrpOp i^i ~P g ) ) $.

  ${
    $d G g h $.  $d H h $.
    $( The predicate "is a subgroup of ` G ` ."  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    issubg $p |- ( H e. ( SubGrpOp ` G ) <->
                   ( G e. GrpOp /\ H e. GrpOp /\ H C_ G ) ) $=
      ( vg cgr wcel csubg cfv wa wss w3a cpw cin wceq inss2 pwexg ssexg sylancr
      cvv cv pm5.32i pweq ineq2d df-subg fvmptg mpdan eleq2d elpwg bitri syl6bb
      elin cdm crab dmmpt ssrab2 eqsstri elfvdm sseldi pm4.71ri 3anass 3bitr4i
      ) ADEZBAFGZEZHVABDEZBAIZHZHVCVAVDVEJVAVCVFVAVCBDAKZLZEZVFVAVBVHBVAVHREZVB
      VHMVAVHVGIVGREVJDVGNADOVHVGRPQCADCSZKZLZVHDRFVKAMVLVGDVKAUAUBCUCZUDUEUFVI
      VDBVGEZHVFBDVGUJVDVOVEBADUGTUHUITVCVAVCFUKZDAVPVMREZCDULDCDVMFVNUMVQCDUNU
      OBAFUPUQURVAVDVEUSUT $.
      $( [12-Jul-2014] $) $( [3-Mar-2008] $)
  $}

  ${
    subgres.1 $e |- W = ran H $.
    $( A subgroup operation is the restriction of its parent group operation to
       its underlying set.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgres $p |- ( H e. ( SubGrpOp ` G ) -> H = ( G |` ( W X. W ) ) ) $=
      ( csubg cfv wcel cxp cres wfun wss cdm wceq cgr crn issubg simp1bi grpofo
      wfo 3syl eqid fofun simp3bi simp2bi fof fdm eqimss2 fun2ssres syl3anc wfn
      wf fofn fnresdm syl eqtr2d ) BAEFGZACCHZIZBUQIZBUPAJZBAKZUQBLZKZURUSMUPAN
      GZAOZVEHZVEASUTUPVDBNGZVAABPZQAVEVEUARVFVEAUBTUPVDVGVAVHUCUPUQCBUKZVBUQMV
      CUPVGUQCBSZVIUPVDVGVAVHUDZBCDRZUQCBUETUQCBUFUQVBUGTUQABUHUIUPVGVJUSBMZVKV
      LVJBUQUJVMUQCBULUQBUMUNTUO $.
      $( [3-Mar-2008] $)

    $( The result of a subgroup operation is the same as the result of its
       parent operation.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgov $p |- ( ( H e. ( SubGrpOp ` G ) /\ ( A e. W /\ B e. W ) ) ->
                   ( A H B ) = ( A G B ) ) $=
      ( csubg cfv wcel wa co cxp cres subgres oveqd ovres sylan9eq ) DCGHIZAEIB
      EIJABDKABCEELMZKABCKRDSABCDEFNOABEECPQ $.
      $( [8-Jul-2014] $) $( [3-Mar-2008] $)
  $}

  ${
    subgrnss.1 $e |- X = ran G $.
    subgrnss.2 $e |- W = ran H $.
    $( The underlying set of a subgroup is a subset of its parent group's
       underlying set.  (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgrnss $p |- ( H e. ( SubGrpOp ` G ) -> W C_ X ) $=
      ( csubg cfv wcel crn wss cima imassrn subgres rneqd df-ima syl6eqr sseq1d
      cxp cres mpbiri 3sstr4g ) BAGHIZBJZAJZCDUCUDUEKACCSZLZUEKAUFMUCUDUGUEUCUD
      AUFTZJUGUCBUHABCFNOAUFPQRUAFEUB $.
      $( [3-Mar-2008] $)
  $}

  ${
    subgid.1 $e |- U = ( Id ` G ) $.
    subgid.2 $e |- T = ( Id ` H ) $.
    $( The identity element of a subgroup is the same as its parent's.
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    subgid $p |- ( H e. ( SubGrpOp ` G ) -> T = U ) $=
      ( csubg cfv wcel wceq co crn cgr wss issubg simp2bi eqid grpoidcl syl2anc
      id syl subgov syl12anc grpolid eqtr3d wb simp1bi subgrnss sseldd grpoid
      mpbird ) DCGHIZABJZAACKZAJZULAADKZUNAULULADLZIZURUPUNJULTULDMIZURULCMIZUS
      DCNZCDOZPZADUQUQQZFRUAZVEAACDUQVDUBUCULUSURUPAJVCVEAADUQVDFUDSUEULUTACLZI
      UMUOUFULUTUSVAVBUGULUQVFACDUQVFVFQZVDUHVEUIABCVFVGEUJSUK $.
      $( [3-Mar-2008] $)
  $}

  ${
    subginv.1 $e |- W = ran H $.
    subginv.2 $e |- M = ( inv ` G ) $.
    subginv.3 $e |- N = ( inv ` H ) $.
    $( The inverse of a subgroup element is the same as its inverse in the
       parent group.  (Contributed by Mario Carneiro, 8-Jul-2014.) $)
    subginv $p |- ( ( H e. ( SubGrpOp ` G ) /\ A e. W ) ->
                    ( N ` A ) = ( M ` A ) ) $=
      ( csubg cfv wcel wceq co cgi cgr wss eqid sylan adantr wa issubg grporinv
      simp2bi simpl simpr grpoinvcl subgov syl12anc subgid 3eqtr3d crn subgrnss
      wb simp1bi sselda sseldd grpoinvid1 syl3anc mpbird eqcomd ) CBJKLZAFLZUAZ
      ADKZAEKZVDVEVFMZAVFBNZBOKZMZVDAVFCNZCOKZVHVIVBCPLZVCVKVLMVBBPLZVMCBQZBCUB
      ZUDZAVLCEFGVLRZIUCSVDVBVCVFFLZVKVHMVBVCUEVBVCUFVBVMVCVSVQACEFGIUGSZAVFBCF
      GUHUIVBVLVIMVCVLVIBCVIRZVRUJTUKVDVNABULZLVFWBLVGVJUNVBVNVCVBVNVMVOVPUOTVB
      FWBABCFWBWBRZGUMZUPVDFWBVFVBFWBQVCWDTVTUQAVFVIBDWBWCWAHURUSUTVA $.
      $( [8-Jul-2014] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d G x y $.  $d H x y $.  $d Y x y $.
    issubgilem.1 $e |- ( ( x e. Y /\ y e. Y ) -> ( x H y ) = ( x G y ) ) $.
    $( Lemma for ~ issubgi . $)
    issubgilem $p |- ( ( A e. Y /\ B e. Y ) -> ( A H B ) = ( A G B ) ) $=
      ( cv co wceq oveq1 eqeq12d oveq2 vtocl2ga ) AIZBIZFJZPQEJZKCQFJZCQEJZKCDF
      JZCDEJZKABCDGGPCKRTSUAPCQFLPCQELMQDKTUBUAUCQDCFNQDCENMHO $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d G a b $.  $d H a b x y $.  $d H b x y z $.  $d N a b y $.
    $d U a b x y $.  $d U b x y z $.  $d Y a b x y $.  $d Y b x y z $.
    issubgi.1 $e |- G e. GrpOp $.
    issubgi.2 $e |- X = ran G $.
    issubgi.3 $e |- U = ( Id ` G ) $.
    issubgi.4 $e |- N = ( inv ` G ) $.
    issubgi.5 $e |- Y C_ X $.
    issubgi.6 $e |- H = ( G |` ( Y X. Y ) ) $.
    issubgi.7 $e |- ( ( x e. Y /\ y e. Y ) -> ( x G y ) e. Y ) $.
    issubgi.8 $e |- U e. Y $.
    issubgi.9 $e |- ( x e. Y -> ( N ` x ) e. Y ) $.
    $( Properties that determine a subgroup.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    issubgi $p |- H e. ( SubGrpOp ` G ) $=
      ( wcel co wceq vz va vb csubg cfv cgr wss issubg cv crn cvv rnexg eqeltri
      ax-mp ssexi cxp wf wfn wral ffnov cres wfo grpofo fof xpss12 mp2an fssres
      mp2b feq1i mpbir wa oveqi ovres syl5eq issubgilem eqeltrd rgen2a mpbir2an
      ffn w3a sseli grpoass mpan syl3an sylan 3impa oveq1d 3adant3 eqtrd sylan2
      fovcl 3impb oveq2d 3adant1 3eqtr4d grpolid sylancr mpancom grpolinv resss
      isgrpoi eqsstri mpbir3an ) EDUDUERDUFRZEUFREDUGDEUHIABUACEAUIZFUEZHHGGDUJ
      ZUKJXDXGUKRIDUFULUNUMMUOHHUPZHEUQEXHURZXEBUIZESZHRZBHUSAHUSABHHHEUTXHGEUQ
      ZXIXMXHGDXHVAZUQZGGUPZGDUQZXHXPUGZXOXDXPGDVBXQIDGJVCXPGDVDVHHGUGZXSXRMMHG
      HGVEVFXPGXHDVGVFXHGEXNNVIVJXHGEVSUNXLABHXEHRZXJHRZVKZXKXEXJDSZHUBUCXEXJDE
      HUBUIZHRUCUIZHRVKYDYEESYDYEXNSYDYEDSEXNYDYENVLYDYEHHDVMVNZVOZOVPZVQVRZXTY
      AUAUIZHRZVTZYCYJDSZXEXJYJDSZDSZXKYJESZXEXJYJESZESZXTXEGRZYAXJGRZYKYJGRZYM
      YOTZHGXEMWAZHGXJMWAHGYJMWAXDYSYTUUAVTUUBIXEXJYJDGJWBWCWDYLYPXKYJDSZYMXTYA
      YKYPUUDTZYBXLYKUUEYHUBUCXKYJDEHYFVOWEWFXTYAUUDYMTYKYBXKYCYJDYGWGWHWIYLYRX
      EYQDSZYOXTYAYKYRUUFTZYAYKVKZXTYQHRUUGXJYJHHHEYIWKUBUCXEYQDEHYFVOWJWLYAYKU
      UFYOTXTUUHYQYNXEDUBUCXJYJDEHYFVOWMWNWIWOPXTCXEESZCXEDSZXECHRXTUUIUUJTPUBU
      CCXEDEHYFVOWCXTXDYSUUJXETIUUCXECDGJKWPWQWIQXTXFXEESZXFXEDSZCXFHRXTUUKUULT
      QUBUCXFXEDEHYFVOWRXTXDYSUULCTIUUCXECDFGJKLWSWQWIXAEXNDNDXHWTXBXC $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d G x y $.  $d H x y $.
    $( A subgroup of an Abelian group is Abelian.  (Contributed by Paul
       Chapman, 25-Apr-2008.) $)
    subgablo $p |- ( ( G e. AbelOp /\ H e. ( SubGrpOp ` G ) )
           -> H e. AbelOp ) $=
      ( vx vy cablo wcel csubg cfv wa cv co wceq crn wral eqid sseld cgr isablo
      subgov adantll simpr subgrnss anim12d wi simprbi ra42 syl sylan9r ancom2s
      imp 3eqtr4d ralrimivva wss issubg simp2bi biimpri sylan syl2anc ) AEFZBAG
      HFZIZUTCJZDJZBKZVCVBBKZLZDBMZNCVGNZBEFZUSUTUAVAVFCDVGVGVAVBVGFZVCVGFZIZIV
      BVCAKZVCVBAKZVDVEVAVLVMVNLZUTVLVBAMZFZVCVPFZIZUSVOUTVJVQVKVRUTVGVPVBABVGV
      PVPOZVGOZUBZPUTVGVPVCWBPUCUSVODVPNCVPNZVSVOUDUSAQFZWCCDAVPVTRUEVOCDVPVPUF
      UGUHUJUTVLVDVMLUSVBVCABVGWASTUTVLVEVNLZUSUTVKVJWEVCVBABVGWASUITUKULUTBQFZ
      VHVIUTWDWFBAUMABUNUOVIWFVHICDBVGWARUPUQUR $.
      $( [25-Apr-2008] $)
  $}

  ${
    $d x y H $.  $d s u v w y z ph $.  $d s u v w z R $.  $d s u v w x y z X $.
    $d u v x y G $.  $d s u v w x y z Y $.
    lagsubg.1 $e |- X = ran G $.
    lagsubg.2 $e |- Y = ran H $.
    ${
      lagsubg.3 $e |- R = { <. x , y >. |
        ( ( x e. X /\ y e. X ) /\ ( x ( /g ` G ) y ) e. Y ) } $.
      lagsubg.4 $e |- ( ph -> H e. ( SubGrpOp ` G ) ) $.
      $( The coset relation is an equivalence relation. $)
      lagsubglem1 $p |- ( ph -> ( Er R /\ dom R = X ) ) $=
        ( wceq cv wbr wa wcel cfv co adantr vu vv vw wer cdm cgs simpr bi2anan9
        vex eleq1 oveq12 eleq1d anbi12d braba sylib simpld simprd jca cgn csubg
        eqid subginv syl2anc cgr wss w3a issubg simp1d grpoinvdiv syl3anc eqtrd
        simp2d grpoinvcl eqeltrrd sylanbrc adantrr syl12anc grponpncan syl13anc
        simprr subgov grpocl iserd cxp copab simpl ssopab2i df-xp 3sstr4i ax-mp
        dmss dmxpid sseqtri a1i cgi subgid syl grpodivid eqtr4d grpoidcl breldm
        sylan ex ssrdv eqssd ) ADUDDUEZGMAUAUBUCDAUANZUBNZDOZPZXHGQZXGGQZPZXHXG
        EUFRZSZHQZXHXGDOXJXKXLXJXLXKXJXLXKPZXGXHXNSZHQZXJXIXQXSPZAXIUGBNZGQZCNZ
        GQZPZYAYCXNSZHQZPZXTBCXGXHDUAUIZUBUIZYAXGMZYCXHMZPZYEXQYGXSYKYBXLYLYDXK
        YAXGGUJZYCXHGUJUHYMYFXRHYAXGYCXHXNUKULUMKUNUOZUPZUQZXJXLXKYPUPZURXJXRFU
        SRZRZXOHXJYTXREUSRZRZXOXJFEUTRQZXSYTUUBMAUUCXILTXJXQXSYOUQZXREFUUAYSHJU
        UAVAZYSVAZVBVCXJEVDQZXLXKUUBXOMAUUGXIAUUGFVDQZFEVEZAUUCUUGUUHUUIVFLEFVG
        UOZVHZTYRYQXGXHXNEUUAGIUUEXNVAZVIVJVKXJUUHXSYTHQAUUHXIAUUGUUHUUIUUJVLZT
        UUDXRFYSHJUUFVMVCVNYHXMXPPBCXHXGDYJYIYAXHMZYCXGMZPZYEXMYGXPUUNYBXKUUOYD
        XLYAXHGUJZYCXGGUJZUHUUPYFXOHYAXHYCXGXNUKULUMKUNVOAXIXHUCNZDOZPZPZXLUUSG
        QZPZXGUUSXNSZHQZXGUUSDOUVBXLUVCAXIXLUUTYRVPZUVBXKUVCUVBXKUVCPZXHUUSXNSZ
        HQZUVBUUTUVHUVJPZAXIUUTVTYHUVKBCXHUUSDYJUCUIZUUNYCUUSMZPZYEUVHYGUVJUUNY
        BXKUVMYDUVCUUQYCUUSGUJZUHUVNYFUVIHYAXHYCUUSXNUKULUMKUNUOZUPZUQZURUVBXRU
        VIFSZUVEHUVBUVSXRUVIESZUVEUVBUUCXSUVJUVSUVTMAUUCUVALTAXIXSUUTUUDVPZUVBU
        VHUVJUVPUQZXRUVIEFHJWAVQUVBUUGXLXKUVCUVTUVEMAUUGUVAUUKTUVGUVBXKUVCUVQUP
        UVRXGXHUUSXNEGIUULVRVSVKUVBUUHXSUVJUVSHQAUUHUVAUUMTUWAUWBXRUVIFHJWBVJVN
        YHUVDUVFPBCXGUUSDYIUVLYKUVMPZYEUVDYGUVFYKYBXLUVMYDUVCYNUVOUHUWCYFUVEHYA
        XGYCUUSXNUKULUMKUNVOWCAXFGXFGVEAXFGGWDZUEZGDUWDVEXFUWEVEYHBCWEYEBCWEDUW
        DYHYEBCYEYGWFWGKBCGGWHWIDUWDWKWJGWLWMWNAUAGXFAXLXGXFQZAXLPZXGXGDOZUWFUW
        GXLXLPZXGXGXNSZHQZUWHUWGXLXLAXLUGZUWLURUWGFWORZUWJHUWGUWMEWORZUWJUWGUUC
        UWMUWNMAUUCXLLTUWMUWNEFUWNVAZUWMVAZWPWQAUUGXLUWJUWNMUUKXGXNUWNEGIUULUWO
        WRXBWSUWGUUHUWMHQAUUHXLUUMTUWMFHJUWPWTWQVNYHUWIUWKPBCXGXGDYIYIYKUUOPZYE
        UWIYGUWKYKYBXLUUOYDXLYNUURUHUWQYFUWJHYAXGYCXGXNUKULUMKUNVOXGXGDYIXAWQXC
        XDXEUR $.
        $( [8-Jul-2014] $)

      lagsubg.5 $e |- ( ph -> X e. Fin ) $.
      $( Lemma for ~ lagsubg . $)
      lagsubglem2 $p |- ( ph -> ( # ` X ) =
        ( ( # ` ( X /. R ) ) x. ( # ` Y ) ) ) $=
        ( chash cfv co wceq wcel wss wa vw vs vz vu vv cqs cuni cfn lagsubglem1
        cmul wer cdm simpld simprd cxp xpfi syl2anc copab opabssxp eqsstri ssfi
        cv cgs sylancl uniqs2 fveq2d ssid wi cpw pwfi sylib qsss c0 sseq1 unieq
        csn cun fveq2 oveq1d eqeq12d imbi12d imbi2d cc0 cn0 cc csubg syl hashcl
        subgrnss nn0cn mul02 3syl eqcomd uni0 fveq2i hash0 eqtri oveq1i 3eqtr4g
        a1d wn ssun1 sstr imim1i caddc oveq1 uniun vex cin adantr simprr syl5ss
        mpan sylibr sseldd ad2antrr eqeltrd syl5bi syl3anc cen wbr eqid cvv cgr
        simplr simpr eleq1 bi2anan9 oveq12 eleq1d anbi12d braba ex a1i wb eqcom
        elec oveq2d c1 a2d unisn uneq2i sstrd sspwuni ssun2 snss simprlr eluni2
        elpwi elin wrex w3a wne simpr3 simpr2 inelcm simpr1 qsdisj ord necon1ad
        mpd 3exp2 rexlimdv imp3a mtod eq0rdv hashun breq2 issubg simp1bi sselda
        cec elex grpocl jca grpopncan ovex sylanbrc ersym ersymb syl5bb simprbi
        syl6bi ecss simprl grpodiveq syl13anc 3bitr4g en3d syldan hashen mpbird
        ectocld eqtr4d syl5eq ax-mp simprll ax-1cn adddir mulid2 3eqtrd syl5ibr
        hashunsng ad2antrl expr syl5 expcom findcard2s mpcom mpi eqtr3d ) AGDUF
        ZUGZNOZGNOUXLNOZHNOZUJPZAUXMGNAGDUHADUKZDULGQZABCDEFGHIJKLUIZUMZAUXRUXS
        UXTUNZAGGUOZUHRZDUYCSDUHRAGUHRZUYEUYDMMGGUPUQDBVBZGRZCVBZGRZTZUYFUYHEVC
        OZPZHRZTZBCURUYCKUYMBCGGUSUTUYCDVAVDZVEVFAUXLUXLSZUXNUXQQZUXLVGUXLUHRZA
        UYPUYQVHZAGVIZUHRZUXLUYTSZUYRAUYEVUAMGVJVKAGDUHUYAUYBUYOVLZUYTUXLVAUQAU
        AVBZUXLSZVUDUGZNOZVUDNOZUXPUJPZQZVHZVHAVMUXLSZVMUGZNOZVMNOZUXPUJPZQZVHZ
        VHAUBVBZUXLSZVUSUGZNOZVUSNOZUXPUJPZQZVHZVHAVUSUCVBZVPZVQZUXLSZVVIUGZNOZ
        VVINOZUXPUJPZQZVHZVHAUYSVHUAUBUCUXLVUDVMQZVUKVURAVVQVUEVULVUJVUQVUDVMUX
        LVNVVQVUGVUNVUIVUPVVQVUFVUMNVUDVMVOVFVVQVUHVUOUXPUJVUDVMNVRVSVTWAWBVUDV
        USQZVUKVVFAVVRVUEVUTVUJVVEVUDVUSUXLVNVVRVUGVVBVUIVVDVVRVUFVVANVUDVUSVOV
        FVVRVUHVVCUXPUJVUDVUSNVRVSVTWAWBVUDVVIQZVUKVVPAVVSVUEVVJVUJVVOVUDVVIUXL
        VNVVSVUGVVLVUIVVNVVSVUFVVKNVUDVVIVOVFVVSVUHVVMUXPUJVUDVVINVRVSVTWAWBVUD
        UXLQZVUKUYSAVVTVUEUYPVUJUYQVUDUXLUXLVNVVTVUGUXNVUIUXQVVTVUFUXMNVUDUXLVO
        VFVVTVUHUXOUXPUJVUDUXLNVRVSVTWAWBAVUQVULAWCWCUXPUJPZVUNVUPAVWAWCAUXPWDR
        ZUXPWERZVWAWCQAHUHRZVWBAUYEHGSZVWDMAFEWFORZVWELEFHGIJWIWGZGHVAUQZHWHWGZ
        UXPWJZUXPWKWLWMVUNVUOWCVUMVMNWNWOWPWQVUOWCUXPUJWPWRWSWTVUSUHRZVVGVUSRZX
        AZTZAVVFVVPAVWNVVFVVPVHVVFVVJVVEVHAVWNTZVVPVVJVUTVVEVUSVVISVVJVUTVUSVVH
        XBZVUSVVIUXLXCXMXDVWOVVJVVEVVOAVWNVVJVVEVVOVHVVEVVOAVWNVVJTZTZVVBUXPXEP
        ZVVDUXPXEPZQVVBVVDUXPXEXFVWRVVLVWSVVNVWTVWRVVLVVAVVGVQZNOZVWSVVKVXANVVK
        VVAVVHUGZVQVXAVUSVVHXGVXCVVGVVAVVGUCXHZUUAUUBWQWOVWRVXBVVBVVGNOZXEPZVWS
        VWRVVAUHRZVVGUHRZVVAVVGXIZVMQVXBVXFQVWRUYEVVAGSZVXGAUYEVWQMXJZVWRVUSUYT
        SVXJVWRVUSUXLUYTVWRVUSVVIUXLVWPAVWNVVJXKZXLZAVUBVWQVUCXJZUUCVUSGUUDVKGV
        VAVAUQVWRUYEVVGGSZVXHVXKVWRVVGUYTRVXOVWRUXLUYTVVGVXNVWRVVHUXLSVVGUXLRZV
        WRVVHVVIUXLVVHVUSUUEVXLXLVVGUXLVXDUUFXNZXOVVGGUUIWGGVVGVAUQZVWRUAVXIVWR
        VUDVXIRZVWLAVWKVWMVVJUUGVXSVUDVVARZVUDVVGRZTVWRVWLVUDVVAVVGUUJVWRVXTVYA
        VWLVXTVUDUDVBZRZUDVUSUUKVWRVYAVWLVHZUDVUDVUSUUHVWRVYCVYDUDVUSVWRVYBVUSR
        ZVYCVYAVWLVWRVYEVYCVYAUULZTZVVGVYBVUSVYGVVGVYBXIZVMUUMZVVGVYBQZVYGVYAVY
        CVYIVWRVYEVYCVYAUUNVWRVYEVYCVYAUUOVUDVVGVYBUUPUQVYGVYJVYHVMVYGVYJVYHVMQ
        VYGGVVGVYBDAUXRVWQVYFUYAXPVWRVXPVYFVXQXJVYGVUSUXLVYBVWRVUTVYFVXMXJVWRVY
        EVYCVYAUUQZXOUURUUSUUTUVAVYKXQUVBUVCXRUVDXRUVEUVFVVAVVGUVGXSVWRUXPVXEVV
        BXEVWRUXPVXEQZHVVGXTYAZAVWQVXPVYMVXQHVUDDUVLZXTYAVYMAUAVVGGDUXLUXLYBVYN
        VVGHXTUVHAVUDGRZTZUDUEHVYNVYBVUDEPZUEVBZVUDUYKPZVYPVWDHYCRAVWDVYOVWHXJH
        UHUVMWGVYPVYBHRZVYQVYNRZVYPVYTTZVUDVYQDYAWUAWUBVYQVUDDGGAUXRVYOVYTUYAXP
        WUBEYDRZVYBGRZVYOVYQGRZAWUCVYOVYTAVWFWUCLVWFWUCFYDRFESEFUVIUVJWGZXPZVYP
        HGVYBAVWEVYOVWGXJUVKZAVYOVYTYEZVYBVUDEGIUVNXSZWUIWUBWUEVYOTZVYQVUDUYKPZ
        HRZVYQVUDDYAWUBWUEVYOWUJWUIUVOWUBWULVYBHWUBWUCWUDVYOWULVYBQWUGWUHWUIVYB
        VUDUYKEGIUYKYBZUVPXSVYPVYTYFXQUYNWUKWUMTBCVYQVUDDVYBVUDEUVQZUAXHZUYFVYQ
        QZUYHVUDQZTZUYJWUKUYMWUMWUQUYGWUEWURUYIVYOUYFVYQGYGUYHVUDGYGZYHWUSUYLWU
        LHUYFVYQUYHVUDUYKYIYJYKKYLUVRUVSVYQVUDDWUOWUPYQXNYMVYPVYRVYNRZVYRVUDDYA
        ZVYSHRZWVAVUDVYRDYAVYPWVBVYRVUDDUEXHZWUPYQVYPVUDVYRDGYCAUXRVYOUYAXJAVYO
        YFVYRYCRVYPWVDYNUVTUWAWVBVYRGRZVYOTZWVCUYNWVFWVCTBCVYRVUDDWVDWUPUYFVYRQ
        ZWURTZUYJWVFUYMWVCWVGUYGWVEWURUYIVYOUYFVYRGYGWUTYHWVHUYLVYSHUYFVYRUYHVU
        DUYKYIYJYKKYLUWBUWCVYPVYTWVATZVYBVYSQZVYRVYQQZYOVYPWVITZVYSVYBQZVYQVYRQ
        ZWVJWVKWVLWUCWVEVYOWUDWVMWVNYOAWUCVYOWVIWUFXPWVLVYNGVYRAVYNGSVYOWVIAVUD
        DGUYAUYBUWDXPVYPVYTWVAXKXOAVYOWVIYEWVLHGVYBAVWEVYOWVIVWGXPVYPVYTWVAUWEX
        OVYRVUDVYBUYKEGIWUNUWFUWGVYBVYSYPVYRVYQYPUWHYMUWIUWMUWJVWRVWDVXHVYLVYMY
        OAVWDVWQVWHXJVXRHVVGUWKUQUWLYRUWNUWOVWRVVNVVCYSXEPZUXPUJPZVVDYSUXPUJPZX
        EPZVWTVWRVVMWVOUXPUJVWNVVMWVOQZAVVJVVGYCRVWNWVSVHVXDVUSVVGYCUXCUWPUXDVS
        VWRVVCWERZYSWERZVWCWVPWVRQVWRVWKVVCWDRWVTAVWKVWMVVJUWQVUSWHVVCWJWLWWAVW
        RUWRYNVWRVWBVWCAVWBVWQVWIXJVWJWGZVVCYSUXPUWSXSVWRWVQUXPVVDXEVWRVWCWVQUX
        PQWWBUXPUWTWGYRUXAVTUXBUXEYTUXFUXGYTUXHUXIUXJUXK $.
        $( [11-Jul-2014] $)
    $}

    $( Lagrange theorem for Groups: the order of any subgroup of a finite group
       is a divisor of the order of the group.  (Contributed by Mario Carneiro,
       11-Jul-2014.) $)
    lagsubg $p |- ( ( H e. ( SubGrpOp ` G ) /\ X e. Fin ) ->
                    ( # ` Y ) || ( # ` X ) ) $=
      ( vx vy cfv wcel cfn wa chash cv co cdivides cz wss syl2anc ssfi cgs cmul
      csubg copab cqs wbr cn0 cpw simpr pwfi sylib wceq eqid lagsubglem1 simpld
      wer cdm simpl simprd xpfi opabssxp sylancl qsss hashcl nn0z 3syl subgrnss
      cxp id syl2anr dvdsmul2 lagsubglem2 breqtrrd ) BAUCIJZCKJZLZDMIZCGNZCJHNZ
      CJLVRVSAUAIODJZLGHUDZUEZMIZVQUBOZCMIPVPWCQJZVQQJZVQWDPUFVPWBKJZWCUGJWEVPC
      UHZKJZWBWHRWGVPVOWIVNVOUIZCUJUKVPCWAKVPWAUPZWAUQCULZVPGHWAABCDEFWAUMZVNVO
      URZUNZUOVPWKWLWOUSVPCCVHZKJZWAWPRWAKJVPVOVOWQWJWJCCUTSVTGHCCVAWPWATVBVCWH
      WBTSWBVDWCVEVFVPDKJZVQUGJWFVOVODCRWRVNVOVIABDCEFVGCDTVJDVDVQVEVFWCVQVKSVP
      GHWAABCDEFWMWNWJVLVM $.
      $( [11-Jul-2014] $)
  $}


