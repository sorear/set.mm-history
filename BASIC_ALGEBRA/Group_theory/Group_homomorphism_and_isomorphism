$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Group homomorphism and isomorphism
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c GrpHom $.

  $( Extend class notation to include the class of group homomorphisms. $)
  cghom $a class GrpHom $.

  ${
    $d f g h s x y $.
    $( Define the set of group homomorphisms from ` g ` to ` h ` .
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    df-ghom $a |- GrpHom = { <. <. g , h >. , s >.
     | ( ( g e. GrpOp /\ h e. GrpOp )
        /\ s = { f | ( f : ran g --> ran h /\ A. x e. ran g A. y e. ran g
                     ( ( f ` x ) h ( f ` y ) ) = ( f ` ( x g y ) ) ) } ) } $.
  $}

  $c GrpIso $.

  $( Extend class notation to include the class of group isomorphisms. $)
  cgiso $a class GrpIso $.

  ${
    $d f g h s $.
    $( Define the set of group isomorphisms from ` g ` to ` h ` .  (Contributed
       by Paul Chapman, 25-Feb-2008.) $)
    df-giso $a |- GrpIso = { <. <. g , h >. , s >.
      | ( ( g e. GrpOp /\ h e. GrpOp )
        /\ s = { f e. ( g GrpHom h ) | f : ran g -1-1-onto-> ran h } ) } $.
  $}

  ${
    $d F f x y $.  $d G f g h x y z $.  $d H f g h x y z $.  $d S h z $.
    elghomlem1.1 $e |- S = { f | ( f : ran G --> ran H /\ A. x e. ran G
      A. y e. ran G ( ( f ` x ) H ( f ` y ) ) = ( f ` ( x G y ) ) ) } $.
    $( Lemma for ~ elghom . $)
    elghomlem1 $p |- ( ( G e. GrpOp /\ H e. GrpOp ) -> ( G GrpHom H ) = S ) $=
      ( vg vh cgr wcel cvv co wceq crn cv cfv wral wf wa vz cghom fabexg syl2an
      rnexg cab rneq feq2d oveq fveq2d eqeq2d raleqbidv anbi12d abbidv feq3 syl
      wb eqeq1d 2ralbidv syl6eqr df-ghom ov2g mpd3an3 ) EJKZFJKZCLKZEFUBMCNVDEO
      ZLKFOZLKVFVEEJUEFJUEAPZDPZQZBPZVJQZFMZVIVLEMZVJQZNZBVGRAVGRZDVGVHLLCGUCUD
      HIUAEFJJHPZOZIPZOZVJSZVKVMWAMZVIVLVSMZVJQZNZBVTRZAVTRZTZDUFCUBVGWBVJSZWDV
      PNZBVGRZAVGRZTZDUFZLVSENZWJWODWQWCWKWIWNWQVTVGWBVJVSEUGZUHWQWHWMAVTVGWRWQ
      WGWLBVTVGWRWQWFVPWDWQWEVOVJVIVLVSEUIUJUKULULUMUNWAFNZWPVGVHVJSZVRTZDUFCWS
      WOXADWSWKWTWNVRWSWBVHNWKWTUQWAFUGWBVHVGVJUOUPWSWLVQABVGVGWSWDVNVPVKVMWAFU
      IURUSUMUNGUTABDHIUAVAVBVC $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( Lemma for ~ elghom . $)
    elghomlem2 $p |- ( ( G e. GrpOp /\ H e. GrpOp ) -> ( F e. ( G GrpHom H )
    <-> ( F : ran G --> ran H /\ A. x e. ran G A. y e. ran G
          ( ( F ` x ) H ( F ` y ) ) = ( F ` ( x G y ) ) ) ) ) $=
      ( cgr wcel wa co crn wf cv cfv wceq wral cvv fveq1 elghomlem1 eleq2d elex
      cghom wb feq1 oveq12d eqeq12d 2ralbidv anbi12d elab2g biimpd mpcom wi fex
      rnexg expcom syl adantrd biimprd syli impbid2 adantr bitrd ) FIJZGIJZKZEF
      GUDLZJECJZFMZGMZENZAOZEPZBOZEPZGLZVMVOFLZEPZQZBVJRAVJRZKZVGVHCEABCDFGHUAU
      BVEVIWBUEVFVEVIWBESJZVIWBECUCWCVIWBVJVKDOZNZVMWDPZVOWDPZGLZVRWDPZQZBVJRAV
      JRZKWBDECSWDEQZWEVLWKWAVJVKWDEUFWLWJVTABVJVJWLWHVQWIVSWLWFVNWGVPGVMWDETVO
      WDETUGVRWDETUHUIUJHUKZULUMWBVEWCVIVEVLWCWAVEVJSJZVLWCUNFIUPVLWNWCVJVKSEUO
      UQURUSWCVIWBWMUTVAVBVCVD $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)
  $}

  ${
    $d F f x y $.  $d G f x y $.  $d H f x y $.  $d X x y $.
    elghom.1 $e |- X = ran G $.
    elghom.2 $e |- W = ran H $.
    $( Membership in the set of group homomorphisms from ` G ` to ` H ` .
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    elghom $p |- ( ( G e. GrpOp /\ H e. GrpOp ) ->
      ( F e. ( G GrpHom H ) <-> ( F : X --> W /\ A. x e. X A. y e. X
        ( ( F ` x ) H ( F ` y ) ) = ( F ` ( x G y ) ) ) ) ) $=
      ( vf cgr wcel wa co crn wf cv cfv wceq wral eqid elghomlem2 feq23i raleqi
      cghom cab ralbii bitri anbi12i syl6bbr ) DKLEKLMCDEUENLDOZEOZCPZAQZCRBQZC
      RENUNUODNZCRSZBUKTZAUKTZMGFCPZUQBGTZAGTZMABUKULJQZPUNVCRUOVCRENUPVCRSBUKT
      AUKTMJUFZJCDEVDUAUBUTUMVBUSGFUKULCHIUCVBURAGTUSVAURAGUQBGUKHUDUGURAGUKHUD
      UHUIUJ $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d F x y $.  $d G x y $.  $d H x y $.  $d X x y $.
    ghomlin.1 $e |- X = ran G $.
    $( Linearity of a group homomorphism.  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    ghomlin $p |- ( ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpHom H ) ) /\
                      ( A e. X /\ B e. X ) ) ->
                    ( ( F ` A ) H ( F ` B ) ) = ( F ` ( A G B ) ) ) $=
      ( vx vy cgr wcel co cv cfv wceq wral wa fveq2 fveq2d eqeq12d cghom w3a wf
      crn eqid elghom biimp3a simprd oveq1d oveq1 oveq2d oveq2 rcla42v mpan9 )
      DJKZEJKZCDEUALKZUBZHMZCNZIMZCNZELZUSVADLZCNZOZIFPHFPZAFKBFKQACNZBCNZELZAB
      DLZCNZOZURFEUDZCUCZVGUOUPUQVOVGQHICDEVNFGVNUEUFUGUHVFVMVHVBELZAVADLZCNZOH
      IABFFUSAOZVCVPVEVRVSUTVHVBEUSACRUIVSVDVQCUSAVADUJSTVABOZVPVJVRVLVTVBVIVHE
      VABCRUKVTVQVKCVABADULSTUMUN $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.
    ghomid.1 $e |- U = ( Id ` G ) $.
    ghomid.2 $e |- T = ( Id ` H ) $.
    $( A group homomorphism maps identity element to identity element.
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    ghomid $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpHom H ) ) ->
                   ( F ` U ) = T ) $=
      ( vx vy cgr wcel co cfv wceq crn wa eqid 3ad2ant1 mpdan cv cghom grpoidcl
      w3a ghomlin grpolid fveq2d eqtrd wb wf wral elghom biimp3a simpld ffvelrn
      jca syl2anc wi grpoid ex 3ad2ant2 mpd mpbird ) DJKZEJKZCDEUALKZUCZBCMZANZ
      VGVGELZVGNZVFVIBBDLZCMZVGVFBDOZKZVNPVIVLNVFVNVNVCVDVNVEBDVMVMQZFUBZRZVQUO
      BBCDEVMVOUDSVCVDVLVGNVEVCVKBCVCVNVKBNVPBBDVMVOFUESUFRUGVFVGEOZKZVHVJUHZVF
      VMVRCUIZVNVSVFWAHTZCMITZCMELWBWCDLCMNIVMUJHVMUJZVCVDVEWAWDPHICDEVRVMVOVRQ
      ZUKULUMVQVMVRBCUNUPVDVCVSVTUQVEVDVSVTVGAEVRWEGURUSUTVAVB $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d w z C $.  $d b c w x y z F $.  $d c w x y z G $.  $d a b c w x y z ph $.
    $d w ch $.  $d a b c x y z H $.  $d b w x y z X $.  $d a b c w x y z Y $.
    $d w D $.  $d w x y z O $.
    ghgrp.1 $e |- ( ph -> F : X -onto-> Y ) $.
    ${
      ghgrplem1.2 $e |- ( ( ph /\ w e. X ) -> ps ) $.
      ghgrplem1.3 $e |- ( C = ( F ` w ) -> ( ch <-> ps ) ) $.
      $( Lemma for ~ ghgrp . $)
      ghgrplem1 $p |- ( ( ph /\ C e. Y ) -> ch ) $=
        ( wcel cv cfv wceq wrex wfo foelrn sylan wa syl5ibrcom rexlimdva syldan
        imp ) AEHLZEDMZFNOZDGPZCAGHFQUEUHIDGHEFRSAUHCAUGCDGAUFGLTCUGBJKUAUBUDUC
        $.
        $( [12-May-2014] $) $( [25-Apr-2008] $)
    $}

    ghgrp.2 $e |- ( ( ph /\ ( x e. X /\ y e. X ) ) ->
                      ( F ` ( x G y ) ) = ( ( F ` x ) O ( F ` y ) ) ) $.
    ghgrp.3 $e |- H = ( O |` ( Y X. Y ) ) $.
    $( Lemma for ~ ghgrp . $)
    ghgrplem2 $p |- ( ( ph /\ ( C e. X /\ D e. X ) ) ->
                       ( F ` ( C G D ) ) = ( ( F ` C ) H ( F ` D ) ) ) $=
      ( vz vw wcel co cfv wceq wa cv wral ralrimivva oveq1 fveq2d fveq2 eqeq12d
      oveq1d oveq2 oveq2d cbvral2v sylib rcla42v mpan9 cxp oveqi wf wfo fof syl
      cres ffvelrn anim12dan sylan ovres syl5eq eqtr4d ) ADJQZEJQZUAZUAZDEGRZFS
      ZDFSZEFSZIRZVOVPHRZAOUBZPUBZGRZFSZVSFSZVTFSZIRZTZPJUCOJUCZVKVNVQTZABUBZCU
      BZGRZFSZWIFSZWJFSZIRZTZCJUCBJUCWGAWPBCJJMUDWPWFVSWJGRZFSZWCWNIRZTBCOPJJWI
      VSTZWLWRWOWSWTWKWQFWIVSWJGUEUFWTWMWCWNIWIVSFUGUIUHWJVTTZWRWBWSWEXAWQWAFWJ
      VTVSGUJUFXAWNWDWCIWJVTFUGUKUHULUMWFWHDVTGRZFSZVOWDIRZTOPDEJJVSDTZWBXCWEXD
      XEWAXBFVSDVTGUEUFXEWCVOWDIVSDFUGUIUHVTETZXCVNXDVQXFXBVMFVTEDGUJUFXFWDVPVO
      IVTEFUGUKUHUNUOVLVRVOVPIKKUPVBZRZVQHXGVOVPNUQVLVOKQZVPKQZUAZXHVQTAJKFURZV
      KXKAJKFUSXLLJKFUTVAXLVIXIVJXJJKDFVCJKEFVCVDVEVOVPKKIVFVAVGVH $.
      $( [12-May-2014] $) $( [25-Apr-2008] $)

    ghgrp.4 $e |- X = ran G $.
    ghgrp.5 $e |- ( ph -> Y C_ A ) $.
    ghgrp.6 $e |- ( ph -> O Fn ( A X. A ) ) $.
    ${
      ghgrp.7 $e |- ( ph -> G e. GrpOp ) $.
      $( The image of a group ` G ` under a group homomorphism ` F ` is a
         group.  This is a stronger result than that usually found in the
         literature, since the target of the homomorphism (operator ` O ` in
         our model) need not have any of the properties of a group as a
         prerequisite.  (Contributed by Paul Chapman, 25-Apr-2008.)  (Revised
         by Mario Carneiro, 12-May-2014.) $)
      ghgrp $p |- ( ph -> H e. GrpOp ) $=
        ( wcel co wceq va vb vc vz cvv wfo crn cgr syl syl5eqel fornex sylc cgi
        rnexg cfv c0 wne wf fof eqid grpoidcl ffvelrn syl2anc ne0i cxp wfn wral
        cv wss xpss12 fnssres fneq1i sylibr adantr ghgrplem2 grpocl 3expb sylan
        cres wa syldan eqeltrrd anassrs oveq2 ghgrplem1 ralrimiva oveq1 ralbidv
        eleq1d ffnov sylanbrc wi simprll simprlr simprr grpoass syl13anc fveq2d
        adantrr syl12anc syl3anc 3eqtr3d oveq1d oveq2d expr impancom eqeq12d ex
        simpl imbi2d 3imp2 cgs simprl grpodivcl grponpcan eqtr3d eqeq1d rcla4ev
        wrex cgn grpoinvcl grpoasscan1 eqeq2 rexbidv anbi12d impr simpld simprd
        jca isgrp2d ) AUAUBUCGUEJAIUERIJEUFZJUERAIFUGZUENAFUHRZYLUERQFUHUNUIUJK
        IJUEEUKULAFUMUOZEUOZJRZJUPUQAIJEURZYNIRZYPAYKYQKIJEUSUIZAYMYRQYNFINYNUT
        VAUIIJYNEVBVCJYOVDUIAGJJVEZVFZUAVHZUBVHZGSZJRZUBJVGZUAJVGYTJGURAHYTVSZY
        TVFZUUAAHDDVEZVFYTUUIVIZUUHPAJDVIZUUKUUJOOJDJDVJVCUUIYTHVKVCYTGUUGMVLVM
        AUUFUAJABVHZEUOZUUCGSZJRZUBJVGUUFBUUBEIJKAUULIRZVTZUUOUBJUUQUUMCVHZEUOZ
        GSZJRZUUOCUUCEIJAYKUUPKVNAUUPUURIRZUVAAUUPUVBVTZVTZUULUURFSZEUOZUUTJABC
        UULUUREFGHIJKLMVOZAUVCUVEIRZUVFJRZAYMUVCUVHQYMUUPUVBUVHUULUURFINVPVQVRZ
        AYQUVHUVIYSIJUVEEVBVRWAWBWCUUCUUSTZUUNUUTJUUCUUSUUMGWDZWIWEWFUUBUUMTZUU
        EUUOUBJUVMUUDUUNJUUBUUMUUCGWGZWIWHWEWFUAUBJJJGWJWKAUUBJRZUUCJRZUCVHZJRZ
        UUDUVQGSZUUBUUCUVQGSZGSZTZAUVOUVPUVRUWBWLZWLZAUVPUVRUUNUVQGSZUUMUVTGSZT
        ZWLZWLUWDBUUBEIJKAUVPUUPUWHAUUPUVRUUTUVQGSZUUMUUSUVQGSZGSZTZWLZWLUUPUWH
        WLCUUCEIJKAUUPUVBUWMAUUPUVBUWMAUVRUVCUWLAUVCUUTUDVHZEUOZGSZUUMUUSUWOGSZ
        GSZTZWLUVCUWLWLUDUVQEIJKAUVCUWNIRZUWSAUVCUWTUWSAUVCUWTVTZVTZUVFUWOGSZUU
        MUURUWNFSZEUOZGSZUWPUWRUXBUVEUWNFSZEUOZUULUXDFSZEUOZUXCUXFUXBUXGUXIEUXB
        YMUUPUVBUWTUXGUXITAYMUXAQVNZAUUPUVBUWTWMZAUUPUVBUWTWNZAUVCUWTWOZUULUURU
        WNFINWPWQWRUXBAUVHUWTUXHUXCTAUXAXIZAUVCUVHUWTUVJWSUXNABCUVEUWNEFGHIJKLM
        VOWTUXBAUUPUXDIRZUXJUXFTUXOUXLUXBYMUVBUWTUXPUXKUXMUXNUURUWNFINVPXAABCUU
        LUXDEFGHIJKLMVOWTXBUXBUVFUUTUWOGAUVCUVFUUTTUWTUVGWSXCUXBUXEUWQUUMGUXBAU
        VBUWTUXEUWQTUXOUXMUXNABCUURUWNEFGHIJKLMVOWTXDXBXEXFUVQUWOTZUWLUWSUVCUXQ
        UWIUWPUWKUWRUVQUWOUUTGWDUXQUWJUWQUUMGUVQUWOUUSGWDXDXGXJWEXFXEXFUVKUWHUW
        MUUPUVKUWGUWLUVRUVKUWEUWIUWFUWKUVKUUNUUTUVQGUVLXCUVKUVTUWJUUMGUUCUUSUVQ
        GWGXDXGXJXJWEXFUVMUWCUWHUVPUVMUWBUWGUVRUVMUVSUWEUWAUWFUVMUUDUUNUVQGUVNX
        CUUBUUMUVTGWGXGXJXJWEXHXKAUVOUVPVTVTZUVQUUBGSZUUCTZUCJXSZUUBUVQGSZUUCTZ
        UCJXSZAUVOUVPUYAUYDVTZAUVPUVQUUMGSZUUCTZUCJXSZUUMUVQGSZUUCTZUCJXSZVTZWL
        UVPUYEWLBUUBEIJKAUVPUUPUYLAUUPUYFUUSTZUCJXSZUYIUUSTZUCJXSZVTZWLUUPUYLWL
        CUUCEIJKAUUPUVBUYQAUUPUVBUYQUVDUYNUYPUVDUURUULFXLUOZSZEUOZJRZUYTUUMGSZU
        USTZUYNUVDYQUYSIRZVUAAYQUVCYSVNZUVDYMUVBUUPVUDAYMUVCQVNZAUUPUVBWOZAUUPU
        VBXMZUURUULUYRFINUYRUTZXNXAZIJUYSEVBVCUVDUYSUULFSZEUOZVUBUUSUVDAVUDUUPV
        ULVUBTAUVCXIZVUJVUHABCUYSUULEFGHIJKLMVOWTUVDVUKUUREUVDYMUVBUUPVUKUURTVU
        FVUGVUHUURUULUYRFINVUIXOXAWRXPUYMVUCUCUYTJUVQUYTTUYFVUBUUSUVQUYTUUMGWGX
        QXRVCUVDUULFXTUOZUOZUURFSZEUOZJRZUUMVUQGSZUUSTZUYPUVDYQVUPIRZVURVUEUVDY
        MVUOIRZUVBVVAVUFUVDYMUUPVVBVUFVUHUULFVUNINVUNUTZYAVCVUGVUOUURFINVPXAZIJ
        VUPEVBVCUVDUULVUPFSZEUOZVUSUUSUVDAUUPVVAVVFVUSTVUMVUHVVDABCUULVUPEFGHIJ
        KLMVOWTUVDVVEUUREUVDYMUUPUVBVVEUURTVUFVUHVUGUULUURFVUNINVVCYBXAWRXPUYOV
        UTUCVUQJUVQVUQTUYIVUSUUSUVQVUQUUMGWDXQXRVCYIXEXFUVKUYLUYQUUPUVKUYHUYNUY
        KUYPUVKUYGUYMUCJUUCUUSUYFYCYDUVKUYJUYOUCJUUCUUSUYIYCYDYEXJWEXFUVMUYEUYL
        UVPUVMUYAUYHUYDUYKUVMUXTUYGUCJUVMUXSUYFUUCUUBUUMUVQGWDXQYDUVMUYCUYJUCJU
        VMUYBUYIUUCUUBUUMUVQGWGXQYDYEXJWEYFZYGUXRUYAUYDVVGYHYJ $.
        $( [12-May-2014] $) $( [25-Apr-2008] $)
    $}

    ghablo.7 $e |- ( ph -> G e. AbelOp ) $.
    $( The image of a Abelian group ` G ` under a group homomorphism ` F ` is
       an Abelian group (Contributed by Paul Chapman, 25-Apr-2008.)  (Revised
       by Mario Carneiro, 12-May-2014.) $)
    ghablo $p |- ( ph -> H e. AbelOp ) $=
      ( wcel co wceq va vb cgr cv crn wral cablo ablogrpo syl ghgrp cdm cxp wss
      wfn fndm resgrprn syl3anc eleq2d anbi12d biimpar cfv adantr simprl simprr
      wa wi ablocom fveq2d ghgrplem2 ancom2s 3eqtr3d oveq2 oveq1 eqeq12d imbi2d
      expr ghgrplem1 impancom impr syldan ralrimivva eqid isablo sylanbrc ) AGU
      CRZUAUDZUBUDZGSZWGWFGSZTZUBGUEZUFUAWKUFGUGRABCDEFGHIJKLMNOPAFUGRZFUCRQFUH
      UIUJZAWJUAUBWKWKAWFWKRZWGWKRZVEZWFJRZWGJRZVEZWJAWSWPAWQWNWRWOAJWKWFAHUKDD
      ULZTZWEJDUMJWKTAHWTUNXAPWTHUOUIWMOHGDJMUPUQZURAJWKWGXBURUSUTAWQWRWJAWRBUD
      ZEVAZWGGSZWGXDGSZTZVFWRWJVFBWFEIJKAWRXCIRZXGAXHXDCUDZEVAZGSZXJXDGSZTZVFXH
      XGVFCWGEIJKAXIIRZXHXMAXHXNXMAXHXNVEZVEZXCXIFSZEVAXIXCFSZEVAZXKXLXPXQXREXP
      WLXHXNXQXRTAWLXOQVBAXHXNVCAXHXNVDXCXIFINVGUQVHABCXCXIEFGHIJKLMVIAXNXHXSXL
      TABCXIXCEFGHIJKLMVIVJVKVJVPWGXJTZXGXMXHXTXEXKXFXLWGXJXDGVLWGXJXDGVMVNVOVQ
      VRWFXDTZWJXGWRYAWHXEWIXFWFXDWGGVMWFXDWGGVLVNVOVQVSVTWAUAUBGWKWKWBWCWD $.
      $( [12-May-2014] $) $( [25-Apr-2008] $)
  $}

  ${
    $d x y F $.  $d x y H $.  $d x y O $.  $d x y S $.  $d x y W $.
    $d x y Z $.  $d x y ph $.
    ghsubgo.1 $e |- ( ph -> S e. ( SubGrpOp ` G ) ) $.
    ghsubgo.2 $e |- X = ran G $.
    ghsubgo.3 $e |- ( ph -> F : X --> Y ) $.
    ghsubgo.4 $e |- ( ph -> Y C_ A ) $.
    ghsubgo.5 $e |- ( ph -> O Fn ( A X. A ) ) $.
    ghsubgo.6 $e |- ( ( ph /\ ( x e. X /\ y e. X ) ) ->
                     ( F ` ( x G y ) ) = ( ( F ` x ) O ( F ` y ) ) ) $.
    ghsubgo.7 $e |- Z = ran S $.
    ghsubgo.8 $e |- W = ( F " Z ) $.
    ghsubgo.9 $e |- H = ( O |` ( W X. W ) ) $.
    $( The image of a subgroup ` S ` of group ` G ` under a group homomorphism
       ` F ` on ` G ` is a group, and furthermore is Abelian if ` S ` is
       Abelian.  (Contributed by Paul Chapman, 25-Apr-2008.)  (Revised by Mario
       Carneiro, 12-May-2014.) $)
    ghsubgolem $p |- ( ph -> ( H e. GrpOp /\
                      ( S e. AbelOp -> H e. AbelOp ) ) ) $=
      ( cgr wcel cablo cres cima wfun cdm wss wfo ffun syl csubgo cfv subgornss
      wi wf wceq fdm sseqtr4d fores syl2anc cv wa ssel2 anim12dan sylan issubgo
      syldan simp2bi grpocl 3expb fvres subgoov fveq2d oveqan12d adantl 3eqtr4d
      eqtrd cxp xpeq12i reseq2i eqtri crn imassrn frn syl5ss sstrd ghgrp adantr
      co adantlr wfn simpr ghablo ex jca ) AHUCUDEUEUDZHUEUDZUQABCDFMUFZEHIMFMU
      GZAFUHZMFUIZUJMXBXAUKZAKLFURZXCPKLFULUMAMKXDAEGUNUOUDZMKUJZNGEMKOTUPUMZAX
      FXDKUSPKLFUTUMVAMFVBVCZABVDZMUDZCVDZMUDZVEZVEZXKXMGWLZFUOZXKFUOZXMFUOZIWL
      ZXKXMEWLZXAUOZXKXAUOZXMXAUOZIWLZAXOXKKUDZXMKUDZVEZXRYAUSAXHXOYIXIXHXLYGXN
      YHMKXKVFMKXMVFVGVHSVJXPYCYBFUOZXRXPYBMUDZYCYJUSAEUCUDZXOYKAXGYLNXGGUCUDYL
      EGUJGEVIVKUMZYLXLXNYKXKXMEMTVLVMVHYBMFVNUMXPYBXQFAXGXOYBXQUSNXKXMGEMTVOVH
      VPVTXOYFYAUSAXLXNYDXSYEXTIXKMFVNXMMFVNVQVRVSZHIJJWAZUFIXBXBWAZUFUBYOYPIJX
      BJXBUAUAWBWCWDZTAXBLDAXBFWEZLFMWFAXFYRLUJPKLFWGUMWHQWIZRYMWJAWSWTAWSVEBCD
      XAEHIMXBAXEWSXJWKAXOYCYFUSWSYNWMYQTAXBDUJWSYSWKAIDDWAWNWSRWKAWSWOWPWQWR
      $.
      $( [12-May-2014] $)

    $( The image of a subgroup ` S ` of group ` G ` under a group homomorphism
       ` F ` on ` G ` is a group.  (Revised by Mario Carneiro, 12-May-2014.) $)
    ghsubgo $p |- ( ph -> H e. GrpOp ) $=
      ( cgr wcel cablo wi ghsubgolem simpld ) AHUCUDEUEUDHUEUDUFABCDEFGHIJKLMNO
      PQRSTUAUBUGUH $.
      $( [12-May-2014] $) $( [25-Apr-2008] $)

    ghsubablo.10 $e |- ( ph -> S e. AbelOp ) $.
    $( The image of an Abelian subgroup ` S ` of group ` G ` under a group
       homomorphism ` F ` on ` G ` is an Abelian group.  (Revised by Mario
       Carneiro, 12-May-2014.) $)
    ghsubablo $p |- ( ph -> H e. AbelOp ) $=
      ( cablo wcel cgr wi ghsubgolem simprd mpd ) AEUDUEZHUDUEZUCAHUFUEUKULUGAB
      CDEFGHIJKLMNOPQRSTUAUBUHUIUJ $.
      $( [12-May-2014] $)
  $}


