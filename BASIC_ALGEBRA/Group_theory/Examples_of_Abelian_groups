$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Examples of Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d u x y z A $.
    ablsn.1 $e |- A e. _V $.
    $( The Abelian group operation for the singleton group. $)
    ablosn $p |- { <. <. A , A >. , A >. } e. AbelOp $=
      ( vx vy cop csn grposn cdm cxp dmsnop xpsn eqtr4i cv wcel wceq co elsn wa
      oveq12 oveq2 oveq1 sylan9eq eqtr4d syl2anb isabloi ) CDAAEZAEFZAFZABGUGHU
      FFUHUHIUFAJAABBKLCMZUHNUIAOZDMZAOZUIUKUGPZUKUIUGPZOUKUHNCAQDAQUJULRUMAAUG
      PZUNUIAUKAUGSUJULUNUKAUGPUOUIAUKUGTUKAAUGUAUBUCUDUE $.
      $( [5-Nov-2006] $)

    $( The identity element of the trivial group.  (Contributed by FL,
       21-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    gidsn $p |- ( Id ` { <. <. A , A >. , A >. } ) = A $=
      ( cop csn cgr wcel cgi wceq grposn opex rnsnop eqcomi eqid grpoidcl elsni
      cfv crn mp2b ) AACZACDZEFTGPZADZFUAAHABIUATUBTQUBSAAAJBKLUAMNUAAOR $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)

    $( The inverse function of the trivial group.  (Contributed by FL,
       21-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    ginvsn $p |- ( inv ` { <. <. A , A >. , A >. } ) = ( _I |` { A } ) $=
      ( cid cfv cop csn cres cgn cvv wcel wceq fvi ax-mp opeq2i sneqi wf1o f1of
      wfn wf mp2b f1ovi ffn fnressn mp2an cgr grposn crn rnsnop eqcomi grpoinvf
      opex eqid fsn mpbi 3eqtr4ri ) AACDZEZFZAAEZFZCAFZGZUSAEFZHDZUQUSUPAAAIJZU
      PAKBAILMNOCIRZVEVBURKIICPIICSVFUAIICQIICUBTBIACUCUDVAVAVDSZVDUTKVCUEJVAVA
      VDPVGABUFVCVDVAVCUGVAUSAAAUKBUHUIVDULUJVAVAVDQTAAVDBBUMUNUO $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    $d x y z A $.
    $( Complex number addition is an Abelian group operation. $)
    cnaddablo $p |- + e. AbelOp $=
      ( vx vy vz caddc cc cc0 cv cneg cnex ax-addopr addass 0cn addid2 negcl co
      wcel wceq addcom mpdan negid eqtr3d isgrpoi cxp fdmi isabloi ) ABDEABCFDA
      GZHZEIJUFBGZCGKLUFMUFNZUFEPZUFUGDOZUGUFDOZFUJUGEPUKULQUIUFUGRSUFTUAUBEEUC
      EDJUDUFUHRUE $.
      $( [5-Nov-2006] $)

    $( The group identity element of complex number addition is zero.
       (Contributed by Steve Rodriguez, 3-Dec-2006.) $)
    cnid $p |- 0 = ( Id ` + ) $=
      ( vy vx vz caddc cgi cfv cv co wceq cc wral crio cc0 wcel cablo cnaddablo
      cgr ablogrpo ax-mp cxp ax-addopr fdmi grporn eqid grpoidval rgen wreu 0cn
      addid2 grpoideu ax-17 a17d oveq1 eqeq1d ralbidv riota2f mp2an mpbi eqtr2i
      wb ) DEFZAGZBGZDHZVCIZBJKZAJLZMDQNZVAVGIDONVHPDRSZBAVADJDJVIJJTJDUAUBUCZV
      AUDUESMVCDHZVCIZBJKZVGMIZVLBJVCUIUFMJNZVFAJUGZVMVNUTUHVHVPVIBADJVJUJSVFVM
      ACJMCGMNAUKVOVMAULVBMIZVEVLBJVQVDVKVCVBMVCDUMUNUOUPUQURUS $.
      $( [21-Dec-2013] $) $( [28-Nov-2006] $)

    $( Value of the group inverse of complex number addition.  (Contributed by
       Steve Rodriguez, 3-Dec-2006.) $)
    addinv $p |- ( A e. CC -> ( ( inv ` + ) ` A ) = -u A ) $=
      ( vy vx cc wcel caddc cgn cfv cc0 wceq crio cneg cgr mpan 0cn wreu eqeq1d
      cv co mpbid cablo cnaddablo ablogrpo ax-mp cxp ax-addopr fdmi grporn cnid
      eqid grpoinvval df-neg oveq1i npcan syl5eq wb negcl negeu mpan2 wa addcom
      cmin reubidva ax-17 a17d oveq1 riota2f syl2anc eqtrd ) ADEZAFGHZHZBRZAFSZ
      IJZBDKZALZFMEZVJVLVPJFUAEVRUBFUCUDZBAIFVKDFDVSDDUEDFUFUGUHUIVKUJUKNVJVQAF
      SZIJZVPVQJZVJVTIAVBSZAFSZIVQWCAFAULUMIDEZVJWDIJOIAUNNUOVJVQDEZVOBDPZWAWBU
      PAUQVJAVMFSZIJZBDPZWGVJWEWJOBAIURUSVJWIVOBDVJVMDEUTWHVNIAVMVAQVCTVOWABCDV
      QCRVQEBVDWFWABVEVMVQJVNVTIVMVQAFVFQVGVHTVI $.
      $( [21-Dec-2013] $) $( [3-Dec-2006] $)

    $( The real numbers under addition comprise a subgroup of the complex
       numbers under addition.  (Contributed by Paul Chapman, 25-Apr-2008.) $)
    readdsubgo $p |- ( + |` ( RR X. RR ) ) e. ( SubGrpOp ` + ) $=
      ( vx vy cc0 caddc cr cxp cres cgn cfv cablo wcel cnaddablo ablogrpo ax-mp
      cc cgr ax-addopr fdmi eqid cv grporn cnid ax-resscn readdcl 0re cneg wceq
      recn addinv syl renegcl eqeltrd issubgoi ) ABCDDEEFGZDHIZOEDJKDPKLDMNZDOU
      POOFODQRUAUBUOSUCUNSATZBTUDUEUQEKZUQUOIZUQUFZEURUQOKUSUTUGUQUHUQUIUJUQUKU
      LUM $.
      $( [25-Apr-2008] $)

    $( The integers under addition comprise a subgroup of the complex numbers
       under addition.  (Contributed by Paul Chapman, 25-Apr-2008.) $)
    zaddsubgo $p |- ( + |` ( ZZ X. ZZ ) ) e. ( SubGrpOp ` + ) $=
      ( vx vy cc0 caddc cz cxp cres cgn cfv cablo wcel cnaddablo ablogrpo ax-mp
      cc cgr ax-addopr fdmi eqid cv grporn cnid zsscn zaddcl 0z cneg zcn addinv
      wceq syl znegcl eqeltrd issubgoi ) ABCDDEEFGZDHIZOEDJKDPKLDMNZDOUPOOFODQR
      UAUBUOSUCUNSATZBTUDUEUQEKZUQUOIZUQUFZEURUQOKUSUTUIUQUGUQUHUJUQUKULUM $.
      $( [25-Apr-2008] $)

    $( Nonzero complex number multiplication is an Abelian group operation.
       (Contributed by Steve Rodriguez, 12-Feb-2007.) $)
    ablomul $p |- ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) e. AbelOp $=
      ( vx vy vz cmul cc cc0 c1 cv co cvv wcel mulnzcnopr wa wceq ovres eldifsn
      wne jca eqtrd eldifi csn cdif cxp cres cdiv cnex ax-mp w3a mulcl ad2ant2r
      difexg mulne0 syl2anb sylibr anim1i 3impa syl oveq1d mulass syl3an eqcomd
      eqeltrd 3adant3 3adant1 oveq2d fovcl eqeltrrd sylan2 3impb 3eqtr3d 3eqtrd
      ax-1cn ax-1ne0 mpbir2an mpan mulid2 recne0 3imtr4i mpancom recid2 isgrpoi
      reccl sylbi fdmi mulcom syl2an ancoms 3eqtr4d isabloi ) ABDEFUAZUBZWKUCZU
      DZWKABCGWMGAHZUEIZWKEJKWKJKUFEWJJUKUGLWNWKKZBHZWKKZCHZWKKZUHZWNWQWMIZWSWM
      IZWNWQDIZWSDIZWNWQWSDIZDIZWNWQWSWMIZWMIZXAXCXBWSDIZXEXAXBWKKZWTMZXCXJNWPW
      RWTXLWPWRMZXKWTXMXBXDWKWNWQWKWKDOZXMXDEKZXDFQZMZXDWKKWPWNEKZWNFQZMZWQEKZW
      QFQZMZXQWRWNEFPZWQEFPXTYCMXOXPXRYAXOXSYBWNWQUIUJWNWQULRUMXDEFPUNVBUOUPXBW
      SWKWKDOUQXAXBXDWSDWPWRXBXDNWTXNVCURSWPXRWRYAWTWSEKXEXGNWNEWJTZWQEWJTZWSEW
      JTWNWQWSUSUTXAXGWNXHDIZXIXAXFXHWNDWRWTXFXHNWPWRWTMZXHXFWQWSWKWKDOZVAVDZVE
      ZXAXGWNXFWMIZYGXIWPWRWTXGYLNZYHWPXFWKKZYMYHXHXFWKYIWQWSWKWKWKWMLVFVGWPYNM
      YLXGWNXFWKWKDOVAVHVIYKXAXFXHWNWMYJVEVJSVKGWKKZGEKGFQVLVMGEFPVNZWPGWNWMIZG
      WNDIZWNYOWPYQYRNYPGWNWKWKDOVOWPXRYRWNNYEWNVPUQSXTWOEKZWOFQZMWPWOWKKZXTYSY
      TWNWBWNVQRYDWOEFPVRZWPWOWNWMIZWOWNDIZGUUAWPUUCUUDNUUBWOWNWKWKDOVSWPXTUUDG
      NYDWNVTWCSWAWLWKWMLWDXMXDWQWNDIZXBWQWNWMIZWPXRYAXDUUENWRYEYFWNWQWEWFXNWRW
      PUUFUUENWQWNWKWKDOWGWHWI $.
      $( [7-Nov-2006] $)

    $( The group identity element of nonzero complex number multiplication is
       one.  (Contributed by Steve Rodriguez, 23-Feb-2007.)  (Revised by Mario
       Carneiro, 17-Dec-2013.) $)
    mulid $p |- ( Id ` ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) ) = 1 $=
      ( vy vx vz cmul cc cc0 csn cdif cxp cres cgi cv co wceq wral crio c1 wcel
      cfv ax-mp cgr cablo ablomul ablogrpo mulnzcnopr fdmi grporn grpoidval wne
      eqid ax-1cn ax-1ne0 mpbir2an ovres mpan eldifi mulid2 syl eqtrd rgen wreu
      eldifsn grpoideu ax-17 a17d oveq1 eqeq1d ralbidv riota2f mp2an mpbi eqtri
      wb ) DEFGZHZVOIZJZKSZALZBLZVQMZVTNZBVOOZAVOPZQVQUARZVRWDNVQUBRWEUCVQUDTZB
      AVRVQVOVQVOWFVPVOVQUEUFUGZVRUJUHTQVTVQMZVTNZBVOOZWDQNZWIBVOVTVORZWHQVTDMZ
      VTQVORZWLWHWMNWNQERQFUIUKULQEFVBUMZQVTVOVODUNUOWLVTERWMVTNVTEVNUPVTUQURUS
      UTWNWCAVOVAZWJWKVMWOWEWPWFBAVQVOWGVCTWCWJACVOQCLQRAVDWNWJAVEVSQNZWBWIBVOW
      QWAWHVTVSQVTVQVFVGVHVIVJVKVL $.
      $( [17-Dec-2013] $) $( [3-Dec-2006] $)
  $}


