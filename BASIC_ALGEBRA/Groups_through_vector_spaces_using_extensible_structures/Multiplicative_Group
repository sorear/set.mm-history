$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Multiplicative Group
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c mulGrp $. $( Multiplicative group $)

  $( Multiplicative group. $)
  cmgp $a class mulGrp $.

  $( Define a structure that puts the multiplication operation of a ring in the
     addition slot.  Note that this will not actually be a group for the
     average ring, or even for a field, but it will be a monoid, and ~ unitgrp
     shows that we get a group if we restrict to the elements that have
     inverses.  This allows us to formalize such notions as "the multiplication
     operation of a ring is a monoid" ( ~ rngmgp ) or "the multiplicative
     identity" in terms of the identity of a monoid ( ~ df-1r ). $)
  df-mgp $a |- mulGrp = ( w e. _V |->
    ( w sSet <. ( +g ` ndx ) , ( .r ` w ) >. ) ) $.

  ${
    $d r x R $.  $d r x T $.
    mgpval.1 $e |- M = ( mulGrp ` R ) $.
    mgpval.2 $e |- T = ( .r ` R ) $.
    $( Value of the multiplication group operation. $)
    mgpval $p |- ( R e. V -> M = ( R sSet <. ( +g ` ndx ) , T >. ) ) $=
      ( vr wcel cvv cnx cplusg cfv cop csts co wceq elex cmgp cv cmulr id fveq2
      syl6eqr opeq2d oveq12d df-mgp ovex fvmpt syl5eq syl ) ADHAIHZCAJKLZBMZNOZ
      PADQUKCARLUNEGAGSZULUOTLZMZNOUNIRUOAPZUOAUQUMNURUAURUPBULURUPATLBUOATUBFU
      CUDUEGUFAUMNUGUHUIUJ $.
      $( [21-Dec-2014] $)

    $( Value of the group operation of the multiplication group. $)
    mgpplusg $p |- T = ( +g ` M ) $=
      ( vx cvv wcel cplusg cfv wceq cnx cop cmulr fveq2d eqtr4d c0 fvprc syl5eq
      cmgp csts co fvex eqeltri plusgid setsid mpan2 mgpval wn syl6eqr pm2.61i
      str0 ) AGHZBCIJZKUMBALIJZBMUAUBZIJZUNUMBGHBUQKBANJZGEANUCUDGBIGAFFUEZUFUG
      UMCUPIABCGDEUHOPUMUIZBQUNUTBURQEANRSUTUNQIJQUTCQIUTCATJQDATRSOIUOFUSULUJP
      UK $.
      $( [21-Dec-2014] $)
  $}

  ${
    $d x R $.
    mgpbas.1 $e |- M = ( mulGrp ` R ) $.
    mgpbas.2 $e |- B = ( Base ` R ) $.
    $( Base set of the multiplication group. $)
    mgpbas $p |- B = ( Base ` M ) $=
      ( vx cvv wcel cbs cfv wceq cnx cplusg wn syl5eq fveq2d eqtr4d fvprc cmgp
      c0 cmulr cop csts baseid csn wne grplem fvex elsnc necon3bbii mpbi necomi
      co setsnid eqid mgpval str0 syl6eqr pm2.61i ) BGHZACIJZKUTABLMJZBUAJZUBUC
      UMZIJZVAUTABIJZVEEVCVBIGBFFUDZVBLIJZVBVHUEHZNVBVHUFUGVIVBVHVBVHLMUHUIUJUK
      ULUNOUTCVDIBVCCGDVCUOUPPQUTNZATVAVJAVFTEBIROVJVATIJTVJCTIVJCBSJTDBSROPIVH
      FVGUQURQUS $.
      $( [21-Dec-2014] $)
  $}

  ${
    mgpress.1 $e |- S = ( R |`s A ) $.
    mgpress.2 $e |- M = ( mulGrp ` R ) $.
    $( Subspace commutes with the multiplication group operator. $)
    mgpress $p |- ( ( R e. V /\ A e. W ) -> ( M |`s A ) = ( mulGrp ` S ) ) $=
      ( wcel wa cbs cfv co cmgp wceq cvv eqid syl3anc cnx csts cress simpr fvex
      wss eqeltri a1i simplr mgpbas ressid2 simpll fveq2d syl6eqr eqtr4d cplusg
      wn cmulr cop cin wne csn grplem elsnc necon3bbii setscom sylancl ressval2
      mpbi mgpval syl oveq1d eqtrd ovex ressmulr ax-mp syl5eq 3eqtr4d pm2.61dan
      ) BEIZAFIZJZBKLZAUDZDAUAMZCNLZOVTWBJZWCDWDWEWBDPIZVSWCDOVTWBUBZWFWEDBNLZP
      HBNUCUEZUFVRVSWBUGZAWAWCDPFWCQZWABDHWAQZUHZUIRWEWDWHDWECBNWEWBVRVSCBOWGVR
      VSWBUJWJAWACBEFGWLUIRUKHULUMVTWBUOZJZBSUNLZBUPLZUQZTMZSKLZAWAURZUQZTMZBXB
      TMZWRTMZWCWDWOVRWPWTUSZXCXEOVRVSWNUJZWPWTUTIZUOXFVAXHWPWTWPWTSUNUCZVBVCVG
      WPWTWQXABEXISKUCVDVEWOWCDXBTMZXCWOWNWFVSWCXJOVTWNUBZWFWOWIUFVRVSWNUGZAWAW
      CDPFWKWMVFRWODWSXBTWOVRDWSOXGBWQDEHWQQZVHVIVJVKWOWDCWRTMZXECPIWDXNOCBAUAM
      PGBAUAVLUECWQWDPWDQABCWQGXMVMVHVNWOCXDWRTWOWNVRVSCXDOXKXGXLAWACBEFGWLVFRV
      JVOVPVQ $.
      $( [10-Jan-2015] $)
  $}


