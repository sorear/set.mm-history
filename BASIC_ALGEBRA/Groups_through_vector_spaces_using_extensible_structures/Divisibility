$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Divisibility
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c ||r $. $( Ring divides relation $)
  $c Unit $. $( Ring unit $)

  $( Ring divides relation. $)
  cdsr $a class ||r $.

  $( Ring unit. $)
  cui $a class Unit $.

  ${
    $d v w x y z $.
    $( Define the (right) divisibility relation in a ring.  Access to the left
       divisibility relation is available through
       ` ( ||r `` ( oppR `` R ) ) ` . $)
    df-dvdsr $a |- ||r = ( w e. _V |-> { <. x , y >. | ( x e. ( Base ` w ) /\
      E. z e. ( Base ` w ) ( z ( .r ` w ) x ) = y ) } ) $.

    $( Define the set of units in a ring. $)
    df-unit $a |- Unit = ( w e. _V |->
      ( `' ( ( ||r ` w ) i^i ( ||r ` ( oppR ` w ) ) ) " { ( 1r ` w ) } ) ) $.
  $}

  ${
    $d w x y z R $.
    reldvdsr.1 $e |- D = ( ||r ` R ) $.
    $( The divides relation is a relation.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    reldvdsr $p |- Rel D $=
      ( vx vw vz vy wrel cdsr cfv cv cbs wcel cmulr co wceq wrex cvv df-dvdsr
      wa relmptopab releqi mpbir ) AHBIJZHDKZEKZLJZMFKUEUFNJOGKPFUGQTEDGRBIDGFE
      SUAAUDCUBUC $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d x y D $.  $d r x y z R $.  $d r x y z T $.  $d x y z X $.  $d x y z Y $.
    $d b r x y z B $.  $d x y ph $.  $d x y Z $.
    dvdsr.1 $e |- B = ( Base ` R ) $.
    dvdsr.2 $e |- D = ( ||r ` R ) $.
    ${
      dvdsr.3 $e |- T = ( .r ` R ) $.
      $( Value of the divides relation.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      dvdsrval $p |- ( R e. V -> D = { <. x , y >. |
        ( x e. B /\ E. z e. B ( z T x ) = y ) } ) $=
        ( vr wcel cvv cv wceq wrex wa cfv cbs co copab elex cmulr fveq2 syl6eqr
        cdsr eleq2d rexeqdv anbi12d oveqd eqeq1d anbi2d bitrd opabbidv df-dvdsr
        rexbidv eqeltri cab eqcom rexbii abbii abrexex a1i opabex3 fvmpt syl5eq
        fvex syl ) FHMFNMZEAOZDMZCOZVKGUAZBOZPZCDQZRZABUBZPFHUCVJEFUGSVSJLFVKLO
        ZTSZMZVMVKVTUDSZUAZVOPZCWAQZRZABUBVSNUGVTFPZWGVRABWHWGVLWECDQZRVRWHWBVL
        WFWIWHWADVKWHWAFTSZDVTFTUEIUFZUHWHWECWADWKUIUJWHWIVQVLWHWEVPCDWHWDVNVOW
        HWCGVMVKWHWCFUDSGVTFUDUEKUFUKULUQUMUNUOABCLUPVQABDDWJNIFTVHURZVQBUSZNMV
        LWMVOVNPZCDQZBUSNVQWOBVPWNCDVNVOUTVAVBCBDVNWLVCURVDVEVFVGVI $.
        $( [6-Jan-2015] $) $( [1-Dec-2014] $)

      $( Value of the divides relation.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      dvdsr $p |- ( Y e. V -> ( X D Y <->
        ( X e. B /\ E. z e. B ( z T X ) = Y ) ) ) $=
        ( vx vy wcel cvv wa cv wceq cdsr elex wbr co wrex wi cop cfv cdm elfvdm
        df-br syl eleq2s sylbi reldvdsr brrelexi jca a1i cbs adantr wb dvdsrval
        copab breqd simpl eleq1d oveq2d eqeq12d rexbidv anbi12d brabga sylan9bb
        simpr eqid ancoms an12s ex pm5.21ndd ) HFNZDONZGONZPZGHCUAZGBNZAQZGEUBZ
        HRZABUCZPZWAVTUDVQWAVRVSWAGHUEZCNVRGHCUIVRWHDSUFZCWHWINDSUGZNVRWHDSUHDW
        JTUJJUKULGHCCDJUMUNUOUPWGVTUDVQWBVTWFWBVRVSWBDUQUGZNZVRWLGDUQUFBGDUQUHI
        UKDWKTUJGBTUOURUPVQVTWAWGUSZVRVQVSWMVRWAGHLQZBNZWCWNEUBZMQZRZABUCZPZLMV
        AZUAZVQVSPWGVRCXAGHLMABCDEOIJKUTVBVSVQXBWGUSWTWGLMGHXAOFWNGRZWQHRZPZWOW
        BWSWFXEWNGBXCXDVCZVDXEWRWEABXEWPWDWQHXEWNGWCEXFVEXCXDVKVFVGVHXAVLVIVMVJ
        VNVOVP $.
        $( [1-Dec-2014] $)

      $( Value of the divides relation.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      dvdsr2 $p |- ( ( X e. B /\ Y e. V ) ->
                     ( X D Y <-> E. z e. B ( z T X ) = Y ) ) $=
        ( wcel wa wbr cv co wceq wrex wb dvdsr adantl ibar adantr bitr4d ) GBLZ
        HFLZMGHCNZUEAOGEPHQABRZMZUHUFUGUISUEABCDEFGHIJKTUAUEUHUISUFUEUHUBUCUD
        $.
        $( [1-Dec-2014] $)

      $( A left-multiple of ` X ` is divisible by ` X ` .  (Contributed by
         Mario Carneiro, 1-Dec-2014.) $)
      dvdsrmul $p |- ( ( X e. B /\ Y e. B ) -> X D ( Y T X ) ) $=
        ( vz wcel wa cv co wceq wrex wbr simpl simpr cvv eqeq1d rcla4ev sylancl
        eqid oveq1 wb ovex dvdsr ax-mp sylanbrc ) EAKZFAKZLZUKJMZEDNZFEDNZOZJAP
        ZEUPBQZUKULRUMULUPUPOZURUKULSUPUDUQUTJFAUNFOUOUPUPUNFEDUEUAUBUCUPTKUSUK
        URLUFFEDUGJABCDTEUPGHIUHUIUJ $.
        $( [1-Dec-2014] $)

      dvdsrmul1.4 $e |- ( ph -> R e. Ring ) $.
      dvdsrmul1.5 $e |- ( ph -> Y e. B ) $.
      dvdsrmul1.6 $e |- ( ph -> Z e. B ) $.
      $( The divisibility relation is preserved under right-multiplication.
         (Contributed by Mario Carneiro, 1-Dec-2014.) $)
      dvdsrmul1 $p |- ( ph -> ( X D Y -> ( X T Z ) D ( Y T Z ) ) ) $=
        ( vx wcel co wceq wa adantr wbr cv wrex wb dvdsr syl crg simpr syl13anc
        rngass eqcomd oveq1 eqeq2d syl5ibcom reximdva rngcl syl3anc ovex dvdsr2
        cvv a1i syl2anc sylibrd expimpd sylbid ) AFGCUAZFBPZOUBZFEQZGRZOBUCZSZF
        HEQZGHEQZCUAZAGBPVFVLUDMOBCDEBFGIJKUEUFAVGVKVOAVGSZVKVHVMEQZVNRZOBUCZVO
        VPVJVROBVPVHBPZSZVQVIHEQZRVJVRWAWBVQWADUGPZVTVGHBPZWBVQRVPWCVTAWCVGLTZT
        VPVTUHVPVGVTAVGUHZTVPWDVTAWDVGNTZTBDEVHFHIKUJUIUKVJWBVNVQVIGHEULUMUNUOV
        PVMBPZVNUTPZVOVSUDVPWCVGWDWHWEWFWGBDEFHIKUPUQWIVPGHEURVAOBCDEUTVMVNIJKU
        SVBVCVDVE $.
        $( [1-Dec-2014] $)
    $}

    $( Closure of a dividing element.  (Contributed by Mario Carneiro,
       5-Dec-2014.) $)
    dvdsrcl $p |- ( X D Y -> X e. B ) $=
      ( vx vy wbr cdm wcel wrel reldvdsr releldm mpan cv wex cvv syl ibi cfv co
      eldmg cmulr wceq wrex wa wb vex eqid dvdsr ax-mp simplbi exlimiv ) DEBJZD
      BKZLZDALZBMUPURBCGNDEBOPURDHQZBJZHRZUSURVBHDBUQUDUAVAUSHVAUSIQDCUEUBZUCUT
      UFIAUGZUTSLVAUSVDUHUIHUJIABCVCSDUTFGVCUKULUMUNUOTT $.
      $( [5-Dec-2014] $)

    dvdsrid.3 $e |- ( ph -> R e. Ring ) $.
    dvdsrid.4 $e |- ( ph -> X e. B ) $.
    $( An element in a (unital) ring divides itself.  (Contributed by Mario
       Carneiro, 1-Dec-2014.) $)
    dvdsrid $p |- ( ph -> X D X ) $=
      ( cur cfv cmulr co wcel wbr crg eqid rngidcl syl syl2anc dvdsrmul rnglidm
      wceq breqtrd ) AEDJKZEDLKZMZECAEBNZUEBNZEUGCOIADPNZUIHBDUEFUEQZRSBCDUFEUE
      FGUFQZUATAUJUHUGEUCHIBDUFUEEFULUKUBTUD $.
      $( [1-Dec-2014] $)

    $( Divisibility is transitive.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    dvdsrtr $p |- ( ph -> ( ( Y D Z /\ Z D X ) -> Y D X ) ) $=
      ( vx vy wbr wa wcel cv co wceq wrex cmulr cfv wb eqid syl anbi1d ad2antrl
      dvdsr pm5.32da bitrd an4 reeanv simplr crg ad2antrr simprl simprr syl3anc
      rngcl dvdsrmul syl2anc rngass syl13anc breqtrd breq2d sylan9bbr syl5ibcom
      wi oveq2 breq2 rexlimdvva syl5bir adantrl expimpd sylbid ancomsd ) AGECNZ
      FGCNZFECNZAVQVROZGBPZLQZGDUAUBZRZESZLBTZOZFBPZMQZFWCRZGSZMBTZOZOZVSAVTWGV
      ROWNAVQWGVRAEBPVQWGUCKLBCDWCBGEHIWCUDZUHUEUFAWGVRWMWAVRWMUCAWFMBCDWCBFGHI
      WOUHUGUIUJWNWAWHOZWFWLOZOAVSWAWHWFWLUKAWPWQVSAWHWQVSVHWAWQWEWKOZMBTLBTAWH
      OZVSWEWKLMBBULWSWRVSLMBBWSWBBPZWIBPZOZOZFWBWJWCRZCNZWRVSXCFWBWIWCRZFWCRZX
      DCXCWHXFBPZFXGCNAWHXBUMZXCDUNPZWTXAXHAXJWHXBJUOZWSWTXAUPZWSWTXAUQZBDWCWBW
      IHWOUSURBCDWCFXFHIWOUTVAXCXJWTXAWHXGXDSXKXLXMXIBDWCWBWIFHWOVBVCVDWKXEFWDC
      NWEVSWKXDWDFCWJGWBWCVIVEWDEFCVJVFVGVKVLVMVNVLVOVP $.
      $( [1-Dec-2014] $)

    dvdsrneg.5 $e |- N = ( invg ` R ) $.
    $( An element divides its negative.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    dvdsrneg $p |- ( ph -> X D ( N ` X ) ) $=
      ( cur cfv cmulr co wcel wbr syl eqid syl2anc cgrp rnggrp rngidcl grpinvcl
      crg dvdsrmul rngnegl breqtrd ) AFDLMZEMZFDNMZOZFEMCAFBPUJBPZFULCQJADUAPZU
      IBPZUMADUEPZUNIDUBRAUPUOIBDUIGUISZUCRBDEUIGKUDTBCDUKFUJGHUKSZUFTABDUKUIEF
      GURUQKIJUGUH $.
      $( [1-Dec-2014] $)

  $}

  ${
    $d r D $.  $d r E $.  $d r O $.  $d r R $.
    unit.1 $e |- U = ( Unit ` R ) $.
    unit.2 $e |- O = ( 1r ` R ) $.
    ${
      unit.3 $e |- D = ( ||r ` R ) $.
      unit.4 $e |- S = ( oppR ` R ) $.
      unit.5 $e |- E = ( ||r ` S ) $.
      $( Property of being a unit of a ring.  A unit is an element that left-
         and right-divides one.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      isunit $p |- ( X e. U <-> ( X D O /\ X E O ) ) $=
        ( vr wcel cvv cui cfv cdsr cur syl6eqr wbr wa cdm elfvdm eleq2s syl cop
        elex df-br sylbi adantr cin ccnv csn cima cv coppr fveq2 fveq2d ineq12d
        wceq cnveqd imaeq1d sneqd imaeq2d eqtrd df-unit fvex inex1 cnvex imaexg
        eqeltri ax-mp fvmpt syl5eq eleq2d wb wss inss1 reldvdsr relss eliniseg2
        wrel mp2 mp2an brin bitri syl6bb pm5.21nii ) GDNZBONZGFAUAZGFEUAZUBZWJB
        PUCZNZWKWPGBPQZDGBPUDHUEBWOUHUFWLWKWMWLGFUGZANZWKGFAUIWSBRUCZNZWKXAWRBR
        QZAWRBRUDJUEBWTUHUFUJUKWKWJGAEULZUMZFUNZUOZNZWNWKDXFGWKDWQXFHMBMUPZRQZX
        HUQQZRQZULZUMZXHSQZUNZUOZXFOPXHBVAZXPXDXOUOXFXQXMXDXOXQXLXCXQXIAXKEXQXI
        XBAXHBRURJTXQXKCRQEXQXJCRXQXJBUQQCXHBUQURKTUSLTUTVBVCXQXOXEXDXQXNFXQXNB
        SQZFXHBSURITVDVEVFMVGXDONXFONXCAEAXBOJBRVHVLVIVJXDXEOVKVMVNVOVPXGGFXCUA
        ZWNXCWCZFONXGXSVQXCAVRAWCXTAEVSABJVTXCAWAWDFXROIBSVHVLXCFGOWBWEGFAEWFWG
        WHWI $.
        $( [1-Dec-2014] $)
    $}

    $( The multiplicative identity is a unit.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    1unit $p |- ( R e. Ring -> O e. U ) $=
      ( crg wcel cdsr cfv wbr coppr cbs eqid id rngidcl dvdsrid opprbas opprrng
      isunit sylanbrc ) AFGZCCAHIZJCCAKIZHIZJCBGUAALIZUBACUEMZUBMZUANUEACUFEOZP
      UAUEUDUCCUEAUCUCMZUFQUDMZAUCUIRUHPUBAUCBUDCCDEUGUIUJST $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d x B $.  $d x R $.  $d x X $.
    unitcl.1 $e |- B = ( Base ` R ) $.
    unitcl.2 $e |- U = ( Unit ` R ) $.
    $( A unit is an element of the base set.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    unitcl $p |- ( X e. U -> X e. B ) $=
      ( wcel cur cfv cdsr wbr coppr eqid isunit simplbi dvdsrcl syl ) DCGZDBHIZ
      BJIZKZDAGRUADSBLIZJIZKTBUBCUCSDFSMTMZUBMUCMNOATBDSEUDPQ $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d y R $.  $d x y S $.  $d x U $.
    opprunit.1 $e |- U = ( Unit ` R ) $.
    opprunit.2 $e |- S = ( oppR ` R ) $.
    $( Being a unit is a symmetric property, so it transfers to the opposite
       ring.  (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    opprunit $p |- U = ( Unit ` S ) $=
      ( vx vy cfv cv cur cdsr wbr wa wcel cbs eqid wceq cvv cmulr co cui elfvdm
      coppr ancom opprbas dvdsrcl cdm oppr1 syl breq2d pm5.21nii opprmul ancoms
      elex eqtrd adantr eqeq12d rexbidva pm5.32i wb dvdsr ax-mp 3bitr4i anbi12i
      wrex fvex bitri isunit eqriv ) FCBUAHZFIZAJHZAKHZLZVKVLBKHZLZMZVKBJHZVOLZ
      VKVRBUCHZKHZLZMZVKCNVKVJNVQVPVNMWCVNVPUDVPVSVNWBVPVKAOHZNZVSWDVOBVKVLWDAB
      EWDPZUEZVOPZUFWDVOBVKVRWGWHUFWEVLVRVKVOWEAOUGZNZVLVRQZVKAOUBWJARNWKAWIUNA
      VLBREVLPZUHUIUIZUJUKWEGIZVKASHZTZVLQZGWDVEZMZWEWNVKVTSHZTZVRQZGWDVEZMZVNW
      BWEWRXCWEWQXBGWDWEWNWDNZMZWPXAVLVRXFWPVKWNBSHZTZXAWDAWOXGBVKWNWFWOPZEXGPZ
      ULXEWEXHXAQWDBXGWTVTWNVKWGXJVTPZWTPZULUMUOWEWKXEWMUPUQURUSVLRNVNWSUTAJVFG
      WDVMAWORVKVLWFVMPZXIVAVBVRRNWBXDUTBJVFGWDWAVTWTRVKVRWDBVTXKWGUEWAPZXLVAVB
      VCVDVGVMABCVOVLVKDWLXMEWHVHVOBVTVJWAVRVKVJPVRPWHXKXNVHVCVI $.
      $( [4-Dec-2014] $)
  $}

  ${
    unitmulcl.1 $e |- U = ( Unit ` R ) $.
    ${
      unitmulcl.2 $e |- T = ( .r ` R ) $.
      $( The product of units is a unit.  (Contributed by Mario Carneiro,
         2-Dec-2014.) $)
      unitmulcl $p |- ( ( R e. Ring /\ X e. U /\ Y e. U ) ->
        ( X T Y ) e. U ) $=
        ( crg wcel co cfv cdsr wbr wa eqid isunit sylib syl wceq syl2anc simpld
        w3a cur coppr simp2 cbs simp1 rngidcl dvdsrmul1 rnglidm breqtrd dvdsrtr
        simp3 unitcl mp2and cmulr simprd opprbas opprrng opprmul rngridm eqtr3d
        mpd 3brtr4d sylanbrc ) AHIZDCIZECIZUBZDEBJZAUCKZALKZMZVJVKAUDKZLKZMZVJC
        IVIVJEVLMEVKVLMZVMVIVJVKEBJZEVLVIDVKVLMZVJVRVLMVIVSDVKVOMZVIVGVSVTNVFVG
        VHUEZVLAVNCVOVKDFVKOZVLOZVNOZVOOZPQZUAVIAUFKZVLABDVKEWGOZWCGVFVGVHUGZVI
        VFVKWGIZWIWGAVKWHWBUHRZVIVHEWGIZVFVGVHUMZWGACEWHFUNRZUIVCVIVFWLVRESWIWN
        WGABVKEWHGWBUJTUKVIVQEVKVOMZVIVHVQWONWMVLAVNCVOVKEFWBWCWDWEPQZUAVIWGVLA
        VKVJEWHWCWIWKULUOVIVJDVOMVTVPVIEDVNUPKZJZVKDWQJZVJDVOVIWOWRWSVOMVIVQWOW
        PUQVIWGVOVNWQEVKDWGAVNWDWHURZWEWQOZVIVFVNHIWIAVNWDUSRZWKVIVGDWGIZWAWGAC
        DWHFUNRZUIVCVIWLXCVJWRSWNXDWGABWQVNEDWHGWDXAUTTVIDVKBJZDWSVIVFXCXEDSWIX
        DWGABVKDWHGWBVATVIWJXCXEWSSWKXDWGABWQVNVKDWHGWDXAUTTVBVDVIVSVTWFUQVIWGV
        OVNVKVJDWTWEXBWKULUOVLAVNCVOVKVJFWBWCWDWEPVE $.
        $( [1-Dec-2014] $)
    $}

    $d x y z G $.  $d m x y z R $.  $d m x y z U $.
    unitgrp.2 $e |- G = ( ( mulGrp ` R ) |`s U ) $.
    $( The base set of the group of units.  (Contributed by Mario Carneiro,
       25-Dec-2014.) $)
    unitgrpbas $p |- U = ( Base ` G ) $=
      ( vx cbs cfv wss wceq cv eqid unitcl ssriv cmgp mgpbas ressbas2 ax-mp ) B
      AGHZIBCGHJFBSSABFKSLZDMNBSCAOHZESAUAUALTPQR $.
      $( [25-Dec-2014] $)

    $( The group of units is a group under multiplication.  (Contributed by
       Mario Carneiro, 2-Dec-2014.) $)
    unitgrp $p |- ( R e. Ring -> G e. Grp ) $=
      ( vx vy vm wcel cfv wceq eqid cv co unitcl wa wbr wrex cvv syl2anc vz crg
      cmulr cur cbs unitgrpbas a1i cplusg cmgp mgpplusg ressplusg unitmulcl w3a
      3anim123i rngass sylan2 1unit rnglidm cdsr coppr simpr isunit adantl fvex
      sylib dvdsr2 sylancl opprbas reeanv simprl ad2antrr dvdsrmul simpl simplr
      wb anbi12d syl13anc simprrl oveq1d opprmul simprrr oveq2d 3eqtr3d rngridm
      eqtrd 3brtr3d eqtr3d breqtrd sylanbrc jca expr rexlimdva expimpd reximdv2
      syl5bir sylbid mpd isgrpde ) AUBIZFGUABAUCJZCAUDJZBCUEJKWSABCDEUFUGWTCUHJ
      KWSBWTAUIJZCEAWTXBXBLWTLZUJUKUGAWTBFMZGMZDXCULXDBIZXEBIZUAMZBIZUMWSXDAUEJ
      ZIZXEXJIZXHXJIZUMXDXEWTNXHWTNXDXEXHWTNWTNKXFXKXGXLXIXMXJABXDXJLZDOZXJABXE
      XNDOXJABXHXNDOUNXJAWTXDXEXHXNXCUOUPABXADXALZUQXFWSXKXAXDWTNXDKXOXJAWTXAXD
      XNXCXPURUPWSXFPZXDXAAUSJZQZXDXAAUTJZUSJZQZPZXEXDWTNZXAKZGBRZXQXFYCWSXFVAX
      RAXTBYAXAXDDXPXRLZXTLZYALZVBVEXQYCYEGXJRZHMZXDXTUCJZNZXAKZHXJRZPZYFXQXSYJ
      YBYOXQXKXASIZXSYJVOXFXKWSXOVCZAUDVDZGXJXRAWTSXDXAXNYGXCVFVGXQXKYQYBYOVOYR
      YSHXJYAXTYLSXDXAXJAXTYHXNVHZYIYLLZVFVGVPYPYEYNPZHXJRZGXJRXQYFYEYNGHXJXJVI
      XQUUCYEGXJBXQXLUUCXGYEPZXQXLPZUUBUUDHXJUUEYKXJIZUUBUUDUUEUUFUUBPZPZXGYEUU
      HXEXAXRQXEXAYAQXGUUHYKXDYKWTNZXEXAXRUUHUUFXKYKUUIXRQUUEUUFUUBVJZXQXKXLUUG
      YRVKZXJXRAWTYKXDXNYGXCVLTUUHXAYKWTNZXEXAWTNZYKXEUUHYDYKWTNZXEUUIWTNZUULUU
      MUUHWSXLXKUUFUUNUUOKXQWSXLUUGWSXFVMVKZXQXLUUGVNZUUKUUJXJAWTXEXDYKXNXCUOVQ
      UUHYDXAYKWTUUEUUFYEYNVRZVSUUHUUIXAXEWTUUHUUIYMXAUUHUUFXKUUIYMKUUJUUKXJAWT
      YLXTYKXDXNXCYHUUAVTTUUEUUFYEYNWAWEZWBWCUUHWSUUFUULYKKUUPUUJXJAWTXAYKXNXCX
      PURTUUHWSXLUUMXEKUUPUUQXJAWTXAXEXNXCXPWDTWCUUSWFUUHXEXDXEYLNZXAYAUUHXLXKX
      EUUTYAQUUQUUKXJYAXTYLXEXDYTYIUUAVLTUUHYDUUTXAUUHXKXLYDUUTKUUKUUQXJAWTYLXT
      XDXEXNXCYHUUAVTTUURWGWHXRAXTBYAXAXEDXPYGYHYIVBWIUURWJWKWLWMWNWOWPWQWR $.
      $( [2-Dec-2014] $)

    unitgrp.3 $e |- O = ( 1r ` R ) $.
    $( The identity of the multiplicative group is ` 1r ` . $)
    unitgrpid $p |- ( R e. Ring -> O = ( 0g ` G ) ) $=
      ( vx crg wcel c0g cfv wceq 1unit cbs wss cv eqid unitcl ssriv rngidss
      mp3an2 mpdan ) AIJZDBJZDCKLMZABDEGNUDBAOLZPUEUFHBUGUGABHQUGRZESTBUGACDFUH
      GUAUBUC $.
      $( [25-Dec-2014] $) $( [2-Dec-2014] $)
  $}

  $c invr $.

  $( Extend class notation with multiplicative inverse. $)
  cinvr $a class invr $.

  $( Define multiplicative inverse. $)
  df-invr $a |- invr = ( r e. _V |-> ( invg `
    ( ( mulGrp ` r ) |`s ( Unit ` r ) ) ) ) $.

  ${
    $d r G $.  $d r R $.
    invrfval.u $e |- U = ( Unit ` R ) $.
    invrfval.g $e |- G = ( ( mulGrp ` R ) |`s U ) $.
    invrfval.i $e |- I = ( invr ` R ) $.
    $( Multiplicative inverse function for a division ring. $)
    invrfval $p |- ( R e. V -> I = ( invg ` G ) ) $=
      ( vr wcel cvv cminusg cfv wceq cinvr cmgp cui cress co fveq2 elex syl6eqr
      cv oveq12d fveq2d df-invr fvex fvmpt syl5eq syl ) AEJAKJZDCLMZNAEUAUKDAOM
      ULHIAIUCZPMZUMQMZRSZLMULKOUMANZUPCLUQUPAPMZBRSCUQUNURUOBRUMAPTUQUOAQMBUMA
      QTFUBUDGUBUEIUFCLUGUHUIUJ $.
      $( [25-Dec-2014] $) $( [21-Sep-2011] $)
  $}

  ${
    unitinvcl.1 $e |- U = ( Unit ` R ) $.
    unitinvcl.2 $e |- I = ( invr ` R ) $.
    $( The inverse of a unit exists and is a unit.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    unitinvcl $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` X ) e. U ) $=
      ( crg wcel wa cmgp cress co cminusg wceq eqid invrfval adantr fveq1d cgrp
      cfv unitgrp unitgrpbas grpinvcl sylan eqeltrd ) AGHZDBHZIZDCTDAJTBKLZMTZT
      ZBUHDCUJUFCUJNUGABUICGEUIOZFPQRUFUISHUGUKBHABUIEULUABUIUJDABUIEULUBUJOUCU
      DUE $.
      $( [2-Dec-2014] $)

    $( The inverse of the inverse of a unit is the same element.  (Contributed
       by Mario Carneiro, 4-Dec-2014.) $)
    unitinvinv $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` ( I ` X ) ) = X ) $=
      ( crg wcel wa cfv cmgp cress co cminusg wceq eqid invrfval adantr fveq12d
      fveq1d cgrp unitgrp unitgrpbas grpinvinv sylan eqtrd ) AGHZDBHZIZDCJZCJDA
      KJBLMZNJZJZULJZDUIUJUMCULUGCULOUHABUKCGEUKPZFQRZUIDCULUPTSUGUKUAHUHUNDOAB
      UKEUOUBBUKULDABUKEUOUCULPUDUEUF $.
      $( [4-Dec-2014] $)

    ${
      rnginvcl.3 $e |- B = ( Base ` R ) $.
      $( The inverse of a unit is an element of the ring.  (Contributed by
         Mario Carneiro, 2-Dec-2014.) $)
      rnginvcl $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` X ) e. B ) $=
        ( crg wcel wa cfv unitinvcl unitcl syl ) BIJECJKEDLZCJPAJBCDEFGMABCPHFN
        O $.
        $( [2-Dec-2014] $)
    $}

    unitinvcl.3 $e |- T = ( .r ` R ) $.
    unitinvcl.4 $e |- O = ( 1r ` R ) $.
    $( A unit times its inverse is the identity.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    unitlinv $p |- ( ( R e. Ring /\ X e. U ) -> ( ( I ` X ) T X ) = O ) $=
      ( crg wcel wa cmgp cfv cress co wceq eqid adantr cminusg c0g cgrp unitgrp
      mgpplusg ressplusg grplinv sylan invrfval fveq1d oveq1d unitgrpid 3eqtr4d
      unitgrpbas ) AKLZFCLZMZFANOZCPQZUAOZOZFBQZUSUBOZFDOZFBQEUOUSUCLUPVBVCRACU
      SGUSSZUDCBUSUTVCFACUSGVEUNCBURUSVEABURURSIUEUFVCSUTSUGUHUQVDVAFBUQFDUTUOD
      UTRUPACUSDKGVEHUITUJUKUOEVCRUPACUSEGVEJULTUM $.
      $( [2-Dec-2014] $)

    $( A unit times its inverse is the identity.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    unitrinv $p |- ( ( R e. Ring /\ X e. U ) -> ( X T ( I ` X ) ) = O ) $=
      ( crg wcel wa cmgp cfv cress co wceq eqid adantr cminusg c0g cgrp unitgrp
      mgpplusg ressplusg grprinv sylan invrfval fveq1d oveq2d unitgrpid 3eqtr4d
      unitgrpbas ) AKLZFCLZMZFFANOZCPQZUAOZOZBQZUSUBOZFFDOZBQEUOUSUCLUPVBVCRACU
      SGUSSZUDCBUSUTVCFACUSGVEUNCBURUSVEABURURSIUEUFVCSUTSUGUHUQVDVAFBUQFDUTUOD
      UTRUPACUSDKGVEHUITUJUKUOEVCRUPACUSEGVEJULTUM $.
      $( [2-Dec-2014] $)
  $}

  ${
    0unit.1 $e |- U = ( Unit ` R ) $.
    0unit.2 $e |- Z = ( 0g ` R ) $.
    0unit.3 $e |- O = ( 1r ` R ) $.
    $( The additive identity is a unit if and only if ` 1 = 0 ` , i.e. we are
       in the zero ring.  (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    0unit $p |- ( R e. Ring -> ( Z e. U <-> O = Z ) ) $=
      ( crg wcel wceq wa cinvr cfv cmulr co eqid unitrinv cbs rnginvcl c0g idi
      rnglz syldan eqtr3d simpr 1unit adantr eqeltrrd impbida ) AHIZDBIZCDJZUJU
      KKDDALMZMZANMZOZCDAUOBUMCDEUMPZUOPZGQUJUKUNARMZIUPDJUSABUMDEUQUSPZSUSAUOU
      NDUTURDATMJFUAUBUCUDUJULKCDBUJULUEUJCBIULABCEGUFUGUHUI $.
      $( [4-Dec-2014] $)
  $}

  ${
    $d x N $.  $d x R $.  $d x X $.
    unitnegcl.1 $e |- U = ( Unit ` R ) $.
    unitnegcl.2 $e |- N = ( invg ` R ) $.
    $( The negative of a unit is a unit.  (Contributed by Mario Carneiro,
       4-Dec-2014.) $)
    unitnegcl $p |- ( ( R e. Ring /\ X e. U ) -> ( N ` X ) e. U ) $=
      ( crg wcel wa cfv cur cdsr wbr eqid adantr dvdsrneg breqtrd isunit mp2and
      dvdsrtr coppr cbs simpl cgrp rnggrp unitcl adantl grpinvcl wceq grpinvinv
      syl2anc syl2an simpr sylib simpld rngidcl opprbas opprrng simprd sylanbrc
      opprneg ) AGHZDBHZIZDCJZAKJZALJZMZVEVFAUAJZLJZMZVEBHVDVEDVGMDVFVGMZVHVDVE
      VECJZDVGVDAUBJZVGACVEVNNZVGNZVBVCUCZVDAUDHZDVNHZVEVNHVBVRVCAUEZOVCVSVBVNA
      BDVOEUFZUGVNACDVOFUHUKZFPVBVRVSVMDUIVCVTWAVNACDVOFUJULZQVDVLDVFVJMZVDVCVL
      WDIVBVCUMVGAVIBVJVFDEVFNZVPVINZVJNZRUNZUOVDVNVGAVFVEDVOVPVQVBVFVNHVCVNAVF
      VOWEUPOZTSVDVEDVJMWDVKVDVEVMDVJVDVNVJVICVEVNAVIWFVOUQZWGVBVIGHVCAVIWFUROZ
      WBACVIWFFVAPWCQVDVLWDWHUSVDVNVJVIVFVEDWJWGWKWITSVGAVIBVJVFVEEWEVPWFWGRUT
      $.
      $( [4-Dec-2014] $)
  $}

  $c /r $.

  $( Extend class notation with ring division. $)
  cdvr $a class /r $.

  ${
    $d r x y $.
    $( Define ring division. $)
    df-rngdv $a |- /r = ( r e. _V
     |-> ( x e. ( Base ` r ) , y e. ( Unit ` r )
         |-> ( x ( .r ` r ) ( ( invr ` r ) ` y ) ) ) ) $.
  $}

  ${
    $d r x y B $.  $d r x y I $.  $d r x y R $.  $d r x y T $.  $d r x y U $.
    $d x y X $.  $d x y Y $.
    rngdvval.b $e |- B = ( Base ` R ) $.
    rngdvval.t $e |- T = ( .r ` R ) $.
    rngdvval.u $e |- U = ( Unit ` R ) $.
    rngdvval.i $e |- I = ( invr ` R ) $.
    rngdvval.d $e |- D = ( /r ` R ) $.
    $( Division operation in a ring.  (Revised by Mario Carneiro,
       2-Dec-2014.) $)
    rngdvfval $p |- ( R e. V ->
      D = ( x e. B , y e. U |-> ( x T ( I ` y ) ) ) ) $=
      ( cvv cfv cbs cui fveq2 syl6eqr vr wcel cv co cmpt2 wceq elex cinvr cmulr
      cdvr eqidd fveq1d oveq123d mpt2eq123dv df-rngdv fvex eqeltri mpt2ex fvmpt
      syl5eq syl ) EIUBEOUBZDABCGAUCZBUCZHPZFUDZUEZUFEIUGVBDEUJPVGNUAEABUAUCZQP
      ZVHRPZVCVDVHUHPZPZVHUIPZUDZUEVGOUJVHEUFZABVIVJVNCGVFVOVIEQPZCVHEQSJTVOVJE
      RPZGVHERSLTVOVCVCVLVEVMFVOVMEUIPFVHEUISKTVOVCUKVOVDVKHVOVKEUHPHVHEUHSMTUL
      UMUNABUAUOABCGVFCVPOJEQUPUQGVQOLERUPUQURUSUTVA $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)

    $( Division operation in a ring.  (Revised by Mario Carneiro,
       2-Dec-2014.) $)
    rngdvval $p |- ( ( X e. B /\ Y e. U ) -> ( X D Y ) = ( X T ( I ` Y ) ) ) $=
      ( vx vy wcel co cv cfv cbs wa cdm wceq elfvdm eleq2s adantr rngdvfval syl
      cmpt2 oveqd oveq1 fveq2 oveq2d eqid ovex ovmpt2 eqtrd ) GAPZHEPZUAZGHBQGH
      NOAENRZORZFSZDQZUIZQGHFSZDQZUTBVEGHUTCTUBZPZBVEUCURVIUSVIGCTSAGCTUDIUEUFN
      OABCDEFVHIJKLMUGUHUJNOGHAEVDVGVEGVCDQVAGVCDUKVBHUCVCVFGDVBHFULUMVEUNGVFDU
      OUPUQ $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)
  $}

  ${
    rngdvcl.b $e |- B = ( Base ` R ) $.
    rngdvcl.o $e |- U = ( Unit ` R ) $.
    rngdvcl.d $e |- D = ( /r ` R ) $.
    $( Closure of division operation. $)
    rngdvcl $p |- ( ( R e. Ring /\ X e. B /\ Y e. U ) -> ( X D Y ) e. B ) $=
      ( crg wcel w3a co cinvr cfv cmulr wceq eqid rngdvval 3adant1 rnginvcl
      3adant2 rngcl syld3an3 eqeltrd ) CJKZEAKZFDKZLEFBMZEFCNOZOZCPOZMZAUGUHUIU
      MQUFABCULDUJEFGULRZHUJRZISTUFUGUHUKAKZUMAKUFUHUPUGACDUJFHUOGUAUBACULEUKGU
      NUCUDUE $.
      $( [2-Jul-2014] $)

    $( The units are closed under division. $)
    unitdvcl $p |- ( ( R e. Ring /\ X e. U /\ Y e. U ) -> ( X D Y ) e. U ) $=
      ( crg wcel w3a co cinvr cfv cmulr wceq unitcl eqid rngdvval sylan 3adant1
      simp1 simp2 unitinvcl 3adant2 unitmulcl syl3anc eqeltrd ) CJKZEDKZFDKZLZE
      FBMZEFCNOZOZCPOZMZDUKULUNURQZUJUKEAKULUSACDEGHRABCUQDUOEFGUQSZHUOSZITUAUB
      UMUJUKUPDKZURDKUJUKULUCUJUKULUDUJULVBUKCDUOFHVAUEUFCUQDEUPHUTUGUHUI $.
      $( [2-Jul-2014] $)

    rngdvcan1.t $e |- T = ( .r ` R ) $.
    $( A cancellation law for division.  ( ~ divcan1 analog.) $)
    rngdvcan1 $p |- ( ( R e. Ring /\ X e. B /\ Y e. U ) ->
      ( ( X D Y ) T Y ) = X ) $=
      ( crg wcel w3a co cfv wceq eqid 3adant2 eqtrd cinvr rngdvval oveq1d simp1
      3adant1 simp2 rnginvcl unitcl 3ad2ant3 rngass syl13anc cur oveq2d rngridm
      unitlinv 3adant3 ) CLMZFAMZGEMZNZFGBOZGDOFGCUAPZPZDOZGDOZFUTVAVDGDURUSVAV
      DQUQABCDEVBFGHKIVBRZJUBUEUCUTVEFVCGDOZDOZFUTUQURVCAMZGAMZVEVHQUQURUSUDUQU
      RUSUFUQUSVIURACEVBGIVFHUGSUSUQVJURACEGHIUHUIACDFVCGHKUJUKUTVHFCULPZDOZFUT
      VGVKFDUQUSVGVKQURCDEVBVKGIVFKVKRZUOSUMUQURVLFQUSACDVKFHKVMUNUPTTT $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)

    $( A cancellation law for division.  ( ~ divcan3 analog.) $)
    rngdvcan3 $p |- ( ( R e. Ring /\ X e. B /\ Y e. U ) ->
      ( ( X T Y ) D Y ) = X ) $=
      ( crg wcel w3a co cfv wceq eqid 3adant2 eqtrd cinvr unitcl 3ad2ant3 rngcl
      syld3an3 simp3 rngdvval simp1 simp2 rnginvcl rngass syl13anc cur unitrinv
      syl2anc oveq2d rngridm 3adant3 ) CLMZFAMZGEMZNZFGDOZGBOZVCGCUAPZPZDOZFVBV
      CAMZVAVDVGQUSUTVAGAMZVHVAUSVIUTACEGHIUBUCZACDFGHKUDUEUSUTVAUFABCDEVEVCGHK
      IVERZJUGUOVBVGFGVFDOZDOZFVBUSUTVIVFAMZVGVMQUSUTVAUHUSUTVAUIVJUSVAVNUTACEV
      EGIVKHUJSACDFGVFHKUKULVBVMFCUMPZDOZFVBVLVOFDUSVAVLVOQUTCDEVEVOGIVKKVORZUN
      SUPUSUTVPFQVAACDVOFHKVQUQURTTT $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)

    $( An associative law for division.  ( ~ divass analog.) $)
    rngdvass $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. U ) ) ->
      ( ( X T Y ) D Z ) = ( X T ( Y D Z ) ) ) $=
      ( crg wcel w3a co cfv wceq syl2anc rngdvval wa cinvr simpr1 simpr2 simpr3
      simpl eqid rnginvcl rngass syl13anc rngcl 3adant3r3 oveq2d 3eqtr4d ) CMNZ
      FANZGANZHENZOZUAZFGDPZHCUBQZQZDPZFGVCDPZDPZVAHBPZFGHBPZDPUTUOUPUQVCANZVDV
      FRUOUSUFZUOUPUQURUCUOUPUQURUDZUTUOURVIVJUOUPUQURUEZACEVBHJVBUGZIUHSACDFGV
      CILUIUJUTVAANZURVGVDRUOUPUQVNURACDFGILUKULVLABCDEVBVAHILJVMKTSUTVHVEFDUTU
      QURVHVERVKVLABCDEVBGHILJVMKTSUMUN $.
      $( [4-Dec-2014] $)
  $}

  ${
    rnginvdv.b $e |- B = ( Base ` R ) $.
    rnginvdv.u $e |- U = ( Unit ` R ) $.
    rnginvdv.d $e |- D = ( /r ` R ) $.
    rnginvdv.o $e |- O = ( 1r ` R ) $.
    rnginvdv.i $e |- I = ( invr ` R ) $.
    $( Write the inverse function in terms of division. $)
    rnginvdv $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` X ) = ( O D X ) ) $=
      ( crg wcel wa co cfv cmulr wceq rngidcl eqid rngdvval sylan syldan eqtr2d
      rnginvcl rnglidm ) CMNZGDNZOFGBPZFGEQZCRQZPZUKUHFANUIUJUMSACFHKTABCULDEFG
      HULUAZILJUBUCUHUIUKANUMUKSACDEGILHUFACULFUKHUNKUGUDUE $.
      $( [2-Jul-2014] $)
  $}

  ${
    $d x y B $.  $d x y z K $.  $d x y z L $.  $d x y z ph $.
    rngidpropd.1 $e |- ( ph -> B = ( Base ` K ) ) $.
    rngidpropd.2 $e |- ( ph -> B = ( Base ` L ) ) $.
    rngidpropd.3 $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
      ( x ( .r ` K ) y ) = ( x ( .r ` L ) y ) ) $.
    ${
      rngidpropd.4 $e |- ( ph -> K e. _V ) $.
      rngidpropd.5 $e |- ( ph -> L e. _V ) $.
      $( The ring identity depends only on the ring's base set and
         multiplication operation. $)
      rngidpropd $p |- ( ph -> ( 1r ` K ) = ( 1r ` L ) ) $=
        ( cmgp cfv c0g cur cbs eqid wcel co cvv mgpbas syl6eq cv cmulr mgpplusg
        wa cplusg oveqi 3eqtr3g fvex a1i grpidpropd wceq rngidval syl 3eqtr4d )
        AELMZNMZFLMZNMZEOMZFOMZABCDUQUSADEPMZUQPMGVCEUQUQQZVCQUAUBADFPMZUSPMHVE
        FUSUSQZVEQUAUBABUCZDRCUCZDRUFUFVGVHEUDMZSVGVHFUDMZSVGVHUQUGMZSVGVHUSUGM
        ZSIVIVKVGVHEVIUQVDVIQUEUHVJVLVGVHFVJUSVFVJQUEUHUIUQTRAELUJUKUSTRAFLUJUK
        ULAETRVAURUMJEVAUQTVDVAQUNUOAFTRVBUTUMKFVBUSTVFVBQUNUOUP $.
        $( [26-Dec-2014] $)
    $}

    $( The divisibility relation depends only on the ring's base set and
       multiplication operation. $)
    dvdsrpropd $p |- ( ph -> ( ||r ` K ) = ( ||r ` L ) ) $=
      ( vz cdsr cfv eqid cv wcel wceq wrex wa wb cvv reldvdsr wbr cop cbs cmulr
      co anassrs eqeq1d anasss rexbidva pm5.32da eleq2d rexeqdv anbi12d 3bitr3d
      an32s vex dvdsr ax-mp 3bitr4g df-br 3bitr3g eqrelrdv ) ACJEKLZFKLZVDEVDMZ
      UAVEFVEMZUAACNZJNZVDUBZVHVIVEUBZVHVIUCZVDOVLVEOAVHEUDLZOZBNZVHEUELZUFZVIP
      ZBVMQZRZVHFUDLZOZVOVHFUELZUFZVIPZBWAQZRZVJVKAVHDOZVRBDQZRWHWEBDQZRVTWGAWH
      WIWJAWHRVRWEBDAVODOZWHVRWESZAWKWHWLAWKWHWLAWKRWHRVQWDVIAWKWHVQWDPIUGUHUIU
      GUPUJUKAWHVNWIVSADVMVHGULAVRBDVMGUMUNAWHWBWJWFADWAVHHULAWEBDWAHUMUNUOVITO
      ZVJVTSJUQZBVMVDEVPTVHVIVMMVFVPMURUSWMVKWGSWNBWAVEFWCTVHVIWAMVGWCMURUSUTVH
      VIVDVAVHVIVEVAVBVC $.
      $( [26-Dec-2014] $)

    $( The set of units depends only on the ring's base set and multiplication
       operation. $)
    unitpropd $p |- ( ph -> ( Unit ` K ) = ( Unit ` L ) ) $=
      ( cfv cdsr wbr wa wcel cbs eqid adantr wceq cmulr co vz cui coppr dvdsrcl
      cv cur wi a1i adantlr cvv cdm elfvdm elex syl adantl eqtr3d eleq2d biimpa
      wb rngidpropd breq2d anbi12d ex pm5.21ndd dvdsrpropd breqd opprbas syl6eq
      3syl ancom2s simprl eleqtrd simprr opprmul syl2anc 3eqtr3d isunit 3bitr4g
      bitrd eqrdv ) AUAEUBJZFUBJZAUAUEZEUFJZEKJZLZWCWDEUCJZKJZLZMZWCFUFJZFKJZLZ
      WCWKFUCJZKJZLZMZWCWANWCWBNAWJWCWKWELZWCWKWHLZMZWQAWCEOJZNZWJWTWJXBUGAWFXB
      WIXAWEEWCWDXAPZWEPZUDQUHWTXBUGAWRXBWSXAWEEWCWKXCXDUDQUHAXBWJWTUSAXBMZWFWR
      WIWSXEWDWKWCWEXEBCDEFADXARZXBGQADFOJZRZXBHQABUEZDNZCUEZDNZMXIXKESJZTZXIXK
      FSJZTZRZXBIUIXBEUJNZAXBEOUKZNXRWCEOULEXSUMUNUOXEWCXGNZFXSNFUJNAXBXTAXAXGW
      CADXAXGGHUPUQURWCFOULFXSUMVIUTZVAXEWDWKWCWHYAVAVBVCVDAWRWMWSWPAWEWLWCWKAB
      CDEFGHIVEVFAWHWOWCWKACBDWGWNADXAWGOJGXAEWGWGPZXCVGVHADXGWNOJHXGFWNWNPZXGP
      ZVGVHAXLXJMZMZXNXPXKXIWGSJZTZXKXIWNSJZTZAXJXLXQIVJYFXKXANXIXANXNYHRYFXKDX
      AAXLXJVKZAXFYEGQZVLYFXIDXAAXLXJVMZYLVLXAEXMYGWGXKXIXCXMPYBYGPVNVOYFXKXGNX
      IXGNXPYJRYFXKDXGYKAXHYEHQZVLYFXIDXGYMYNVLXGFXOYIWNXKXIYDXOPYCYIPVNVOVPVEV
      FVBVSWEEWGWAWHWDWCWAPWDPXDYBWHPVQWLFWNWBWOWKWCWBPWKPWLPYCWOPVQVRVT $.
      $( [26-Dec-2014] $)

    invrpropd.4 $e |- ( ph -> K e. _V ) $.
    invrpropd.5 $e |- ( ph -> L e. _V ) $.
    $( The ring inverse function depends only on the ring's base set and
       multiplication operation. $)
    invrpropd $p |- ( ph -> ( invr ` K ) = ( invr ` L ) ) $=
      ( cfv cress co cbs wceq eqid a1i wcel cvv cmgp cinvr unitgrpbas unitpropd
      cminusg syl6eq cv wa cmulr cplusg unitcl ssriv syl5sseqr sselda anim12dan
      cui syldan mgpplusg ressplusg oveqi 3eqtr3g ovex grpinvpropd invrfval syl
      3eqtr4d ) AEUALZEUPLZMNZUELZFUALZFUPLZMNZUELZEUBLZFUBLZABCVHVIVMVHVIOLPAE
      VHVIVHQZVIQZUCRAVHVLVMOLABCDEFGHIUDFVLVMVLQZVMQZUCUFABUGZVHSZCUGZVHSZUHZU
      HWAWCEUILZNZWAWCFUILZNZWAWCVIUJLZNWAWCVMUJLZNAWEWADSZWCDSZUHWGWIPAWBWLWDW
      MAVHDWAAEOLZVHDBVHWNWNEVHWAWNQVQUKULGUMZUNAVHDWCWOUNUOIUQWFWJWAWCVHWFVGVI
      VREWFVGVGQWFQURUSUTWHWKWAWCVLWHVKVMVTFWHVKVKQWHQURUSUTVAVITSAVGVHMVBRVMTS
      AVKVLMVBRVCAETSVOVJPJEVHVIVOTVQVRVOQVDVEAFTSVPVNPKFVLVMVPTVSVTVPQVDVEVF
      $.
      $( [26-Dec-2014] $)
  $}

