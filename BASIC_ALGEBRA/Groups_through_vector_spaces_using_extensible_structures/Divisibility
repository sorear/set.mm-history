$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Divisibility
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c ||r $. $( Ring divides relation $)
  $c Unit $. $( Ring unit $)

  $( Ring divides relation. $)
  cdsr $a class ||r $.

  $( Ring unit. $)
  cui $a class Unit $.

  ${
    $d v w x y z $.
    $( Define the (left) divisibility relation in a ring.  Access to the right
       divisibility relation is available through
       ` ( ||r `` ( oppR `` R ) ) ` . $)
    df-dvdsr $a |- ||r = ( w e. _V |-> { <. x , y >. | ( x e. ( Base ` w ) /\
      E. z e. ( Base ` w ) ( x ( .r ` w ) z ) = y ) } ) $.

    $( Define the set of units in a ring. $)
    df-unit $a |- Unit = ( w e. _V |->
      ( `' ( ( ||r ` w ) i^i ( ||r ` ( oppR ` w ) ) ) " { ( 1r ` w ) } ) ) $.
  $}

  ${
    $d w x y z R $.
    reldvdsr.1 $e |- D = ( ||r ` R ) $.
    $( The divides relation is a relation.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    reldvdsr $p |- Rel D $=
      ( vx vw vz vy wrel cdsr cfv cv cbs wcel cmulr co wceq wrex cvv df-dvdsr
      wa relmptopab releqi mpbir ) AHBIJZHDKZEKZLJZMUEFKUFNJOGKPFUGQTEDGRBIDGFE
      SUAAUDCUBUC $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d x y D $.  $d r x y z R $.  $d r x y z T $.  $d x y z X $.  $d x y z Y $.
    $d b r x y z B $.  $d x y ph $.  $d x y Z $.
    dvdsr.1 $e |- B = ( Base ` R ) $.
    dvdsr.2 $e |- D = ( ||r ` R ) $.
    ${
      dvdsr.3 $e |- T = ( .r ` R ) $.
      $( Value of the divides relation.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      dvdsrval $p |- ( R e. V -> D = { <. x , y >. |
        ( x e. B /\ E. z e. B ( x T z ) = y ) } ) $=
        ( vr wcel cvv cv wceq wrex wa cfv cbs co copab elex cmulr fveq2 syl6eqr
        cdsr eleq2d rexeqdv anbi12d oveqd eqeq1d anbi2d bitrd opabbidv df-dvdsr
        rexbidv eqeltri cab eqcom rexbii abbii abrexex a1i opabex3 fvmpt syl5eq
        fvex syl ) FHMFNMZEAOZDMZVKCOZGUAZBOZPZCDQZRZABUBZPFHUCVJEFUGSVSJLFVKLO
        ZTSZMZVKVMVTUDSZUAZVOPZCWAQZRZABUBVSNUGVTFPZWGVRABWHWGVLWECDQZRVRWHWBVL
        WFWIWHWADVKWHWAFTSZDVTFTUEIUFZUHWHWECWADWKUIUJWHWIVQVLWHWEVPCDWHWDVNVOW
        HWCGVKVMWHWCFUDSGVTFUDUEKUFUKULUQUMUNUOABCLUPVQABDDWJNIFTVHURZVQBUSZNMV
        LWMVOVNPZCDQZBUSNVQWOBVPWNCDVNVOUTVAVBCBDVNWLVCURVDVEVFVGVI $.
        $( [1-Dec-2014] $)

      $( Value of the divides relation.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      dvdsr $p |- ( Y e. V -> ( X D Y <->
        ( X e. B /\ E. z e. B ( X T z ) = Y ) ) ) $=
        ( vx vy wcel cvv wa cv wceq cdsr elex wbr co wrex wi cop cfv cdm elfvdm
        df-br syl eleq2s sylbi reldvdsr brrelexi jca a1i cbs adantr wb dvdsrval
        copab breqd simpl eleq1d oveq1d eqeq12d rexbidv anbi12d brabga sylan9bb
        simpr eqid ancoms an12s ex pm5.21ndd ) HFNZDONZGONZPZGHCUAZGBNZGAQZEUBZ
        HRZABUCZPZWAVTUDVQWAVRVSWAGHUEZCNVRGHCUIVRWHDSUFZCWHWINDSUGZNVRWHDSUHDW
        JTUJJUKULGHCCDJUMUNUOUPWGVTUDVQWBVTWFWBVRVSWBDUQUGZNZVRWLGDUQUFBGDUQUHI
        UKDWKTUJGBTUOURUPVQVTWAWGUSZVRVQVSWMVRWAGHLQZBNZWNWCEUBZMQZRZABUCZPZLMV
        AZUAZVQVSPWGVRCXAGHLMABCDEOIJKUTVBVSVQXBWGUSWTWGLMGHXAOFWNGRZWQHRZPZWOW
        BWSWFXEWNGBXCXDVCZVDXEWRWEABXEWPWDWQHXEWNGWCEXFVEXCXDVKVFVGVHXAVLVIVMVJ
        VNVOVP $.
        $( [1-Dec-2014] $)

      $( Value of the divides relation.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      dvdsr2 $p |- ( ( X e. B /\ Y e. V ) ->
                     ( X D Y <-> E. z e. B ( X T z ) = Y ) ) $=
        ( wcel wa wbr cv co wceq wrex wb dvdsr adantl ibar adantr bitr4d ) GBLZ
        HFLZMGHCNZUEGAOEPHQABRZMZUHUFUGUISUEABCDEFGHIJKTUAUEUHUISUFUEUHUBUCUD
        $.
        $( [1-Dec-2014] $)

      $( A right-multiple of ` X ` is divisible by ` X ` .  (Contributed by
         Mario Carneiro, 1-Dec-2014.) $)
      dvdsrmul $p |- ( ( X e. B /\ Y e. B ) -> X D ( X T Y ) ) $=
        ( vz wcel wa cv co wceq wrex wbr simpl simpr cvv eqeq1d rcla4ev sylancl
        eqid oveq2 wb ovex dvdsr ax-mp sylanbrc ) EAKZFAKZLZUKEJMZDNZEFDNZOZJAP
        ZEUPBQZUKULRUMULUPUPOZURUKULSUPUDUQUTJFAUNFOUOUPUPUNFEDUEUAUBUCUPTKUSUK
        URLUFEFDUGJABCDTEUPGHIUHUIUJ $.
        $( [1-Dec-2014] $)

      dvdsrmul2.4 $e |- ( ph -> R e. Ring ) $.
      dvdsrmul2.5 $e |- ( ph -> Y e. B ) $.
      dvdsrmul2.6 $e |- ( ph -> Z e. B ) $.
      $( The divisibility relation is preserved under left-multiplication.
         (Contributed by Mario Carneiro, 1-Dec-2014.) $)
      dvdsrmul2 $p |- ( ph -> ( X D Y -> ( Z T X ) D ( Z T Y ) ) ) $=
        ( vx wcel co wceq wa adantr wbr cv wrex wb dvdsr syl crg simpr syl13anc
        rngass oveq2 eqeq2d syl5ibcom reximdva cvv rngcl syl3anc dvdsr2 syl2anc
        ovex a1i sylibrd expimpd sylbid ) AFGCUAZFBPZFOUBZEQZGRZOBUCZSZHFEQZHGE
        QZCUAZAGBPVEVKUDMOBCDEBFGIJKUEUFAVFVJVNAVFSZVJVLVGEQZVMRZOBUCZVNVOVIVQO
        BVOVGBPZSZVPHVHEQZRZVIVQVTDUGPZHBPZVFVSWBVOWCVSAWCVFLTZTVOWDVSAWDVFNTZT
        VOVFVSAVFUHZTVOVSUHBDEHFVGIKUJUIVIWAVMVPVHGHEUKULUMUNVOVLBPZVMUOPZVNVRU
        DVOWCWDVFWHWEWFWGBDEHFIKUPUQWIVOHGEUTVAOBCDEUOVLVMIJKURUSVBVCVD $.
        $( [1-Dec-2014] $)
    $}

    $( Closure of a dividing element.  (Contributed by Mario Carneiro,
       5-Dec-2014.) $)
    dvdsrcl $p |- ( X D Y -> X e. B ) $=
      ( vx vy wbr cdm wcel wrel reldvdsr releldm mpan cv wex cvv syl ibi cfv co
      eldmg cmulr wceq wrex wa wb vex eqid dvdsr ax-mp simplbi exlimiv ) DEBJZD
      BKZLZDALZBMUPURBCGNDEBOPURDHQZBJZHRZUSURVBHDBUQUDUAVAUSHVAUSDIQCUEUBZUCUT
      UFIAUGZUTSLVAUSVDUHUIHUJIABCVCSDUTFGVCUKULUMUNUOTT $.
      $( [5-Dec-2014] $)

    dvdsrid.3 $e |- ( ph -> R e. Ring ) $.
    dvdsrid.4 $e |- ( ph -> X e. B ) $.
    $( An element in a (unital) ring divides itself.  (Contributed by Mario
       Carneiro, 1-Dec-2014.) $)
    dvdsrid $p |- ( ph -> X D X ) $=
      ( cur cfv cmulr co wcel wbr crg eqid rngidcl syl syl2anc dvdsrmul rngridm
      wceq breqtrd ) AEEDJKZDLKZMZECAEBNZUEBNZEUGCOIADPNZUIHBDUEFUEQZRSBCDUFEUE
      FGUFQZUATAUJUHUGEUCHIBDUFUEEFULUKUBTUD $.
      $( [1-Dec-2014] $)

    $( Divisibility is transitive.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    dvdsrtr $p |- ( ph -> ( ( Y D Z /\ Z D X ) -> Y D X ) ) $=
      ( vx vy wbr wa wcel cv co wceq wrex cmulr cfv wb eqid syl anbi1d ad2antrl
      dvdsr pm5.32da bitrd an4 reeanv simplr crg ad2antrr simprr simprl syl3anc
      wi rngcl dvdsrmul syl2anc rngass syl13anc breqtrrd oveq1 breq2d sylan9bbr
      breq2 syl5ibcom rexlimdvva syl5bir adantrl expimpd sylbid ancomsd ) AGECN
      ZFGCNZFECNZAVQVROZGBPZGLQZDUAUBZRZESZLBTZOZFBPZFMQZWCRZGSZMBTZOZOZVSAVTWG
      VROWNAVQWGVRAEBPVQWGUCKLBCDWCBGEHIWCUDZUHUEUFAWGVRWMWAVRWMUCAWFMBCDWCBFGH
      IWOUHUGUIUJWNWAWHOZWFWLOZOAVSWAWHWFWLUKAWPWQVSAWHWQVSUSWAWQWEWKOZMBTLBTAW
      HOZVSWEWKLMBBULWSWRVSLMBBWSWBBPZWIBPZOZOZFWJWBWCRZCNZWRVSXCFFWIWBWCRZWCRZ
      XDCXCWHXFBPZFXGCNAWHXBUMZXCDUNPZXAWTXHAXJWHXBJUOZWSWTXAUPZWSWTXAUQZBDWCWI
      WBHWOUTURBCDWCFXFHIWOVAVBXCXJWHXAWTXDXGSXKXIXLXMBDWCFWIWBHWOVCVDVEWKXEFWD
      CNWEVSWKXDWDFCWJGWBWCVFVGWDEFCVIVHVJVKVLVMVNVLVOVP $.
      $( [1-Dec-2014] $)

    dvdsrneg.5 $e |- N = ( invg ` R ) $.
    $( An element divides its negative.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    dvdsrneg $p |- ( ph -> X D ( N ` X ) ) $=
      ( cur cfv cmulr co wcel wbr syl eqid syl2anc cgrp rnggrp rngidcl grpinvcl
      crg dvdsrmul rngnegr breqtrd ) AFFDLMZEMZDNMZOZFEMCAFBPUJBPZFULCQJADUAPZU
      IBPZUMADUEPZUNIDUBRAUPUOIBDUIGUISZUCRBDEUIGKUDTBCDUKFUJGHUKSZUFTABDUKUIEF
      GURUQKIJUGUH $.
      $( [1-Dec-2014] $)

  $}

  ${
    $d r D $.  $d r E $.  $d r O $.  $d r R $.
    unit.1 $e |- U = ( Unit ` R ) $.
    unit.2 $e |- O = ( 1r ` R ) $.
    ${
      unit.3 $e |- D = ( ||r ` R ) $.
      unit.4 $e |- S = ( oppR ` R ) $.
      unit.5 $e |- E = ( ||r ` S ) $.
      $( Property of being a unit of a ring.  A unit is an element that left-
         and right-divides one.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      isunit $p |- ( X e. U <-> ( X D O /\ X E O ) ) $=
        ( vr wcel cvv cui cfv cdsr cur syl6eqr wbr wa cdm elfvdm eleq2s syl cop
        elex df-br sylbi adantr cin ccnv csn cima cv coppr fveq2 fveq2d ineq12d
        wceq cnveqd imaeq1d sneqd imaeq2d eqtrd df-unit fvex inex1 cnvex imaexg
        eqeltri ax-mp fvmpt syl5eq eleq2d wb wss inss1 reldvdsr relss eliniseg2
        wrel mp2 mp2an brin bitri syl6bb pm5.21nii ) GDNZBONZGFAUAZGFEUAZUBZWJB
        PUCZNZWKWPGBPQZDGBPUDHUEBWOUHUFWLWKWMWLGFUGZANZWKGFAUIWSBRUCZNZWKXAWRBR
        QZAWRBRUDJUEBWTUHUFUJUKWKWJGAEULZUMZFUNZUOZNZWNWKDXFGWKDWQXFHMBMUPZRQZX
        HUQQZRQZULZUMZXHSQZUNZUOZXFOPXHBVAZXPXDXOUOXFXQXMXDXOXQXLXCXQXIAXKEXQXI
        XBAXHBRURJTXQXKCRQEXQXJCRXQXJBUQQCXHBUQURKTUSLTUTVBVCXQXOXEXDXQXNFXQXNB
        SQZFXHBSURITVDVEVFMVGXDONXFONXCAEAXBOJBRVHVLVIVJXDXEOVKVMVNVOVPXGGFXCUA
        ZWNXCWCZFONXGXSVQXCAVRAWCXTAEVSABJVTXCAWAWDFXROIBSVHVLXCFGOWBWEGFAEWFWG
        WHWI $.
        $( [1-Dec-2014] $)
    $}

    $( The multiplicative identity is a unit.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    1unit $p |- ( R e. Ring -> O e. U ) $=
      ( crg wcel cdsr cfv wbr coppr cbs eqid id rngidcl dvdsrid opprbas opprrng
      isunit sylanbrc ) AFGZCCAHIZJCCAKIZHIZJCBGUAALIZUBACUEMZUBMZUANUEACUFEOZP
      UAUEUDUCCUEAUCUCMZUFQUDMZAUCUIRUHPUBAUCBUDCCDEUGUIUJST $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d x B $.  $d x R $.  $d x X $.
    unitcl.1 $e |- B = ( Base ` R ) $.
    unitcl.2 $e |- U = ( Unit ` R ) $.
    $( A unit is an element of the base set.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    unitcl $p |- ( X e. U -> X e. B ) $=
      ( wcel cur cfv cdsr wbr coppr eqid isunit simplbi dvdsrcl syl ) DCGZDBHIZ
      BJIZKZDAGRUADSBLIZJIZKTBUBCUCSDFSMTMZUBMUCMNOATBDSEUDPQ $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d y R $.  $d x y S $.  $d x U $.
    opprunit.1 $e |- U = ( Unit ` R ) $.
    opprunit.2 $e |- S = ( oppR ` R ) $.
    $( Being a unit is a symmetric property, so it transfers to the opposite
       ring.  (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    opprunit $p |- U = ( Unit ` S ) $=
      ( vx vy cfv cv cur cdsr wbr wa wcel cbs eqid wceq cvv cmulr co cui elfvdm
      coppr ancom opprbas dvdsrcl cdm oppr1 syl breq2d pm5.21nii opprmul ancoms
      elex eqtrd adantr eqeq12d rexbidva pm5.32i wb dvdsr ax-mp 3bitr4i anbi12i
      wrex fvex bitri isunit eqriv ) FCBUAHZFIZAJHZAKHZLZVKVLBKHZLZMZVKBJHZVOLZ
      VKVRBUCHZKHZLZMZVKCNVKVJNVQVPVNMWCVNVPUDVPVSVNWBVPVKAOHZNZVSWDVOBVKVLWDAB
      EWDPZUEZVOPZUFWDVOBVKVRWGWHUFWEVLVRVKVOWEAOUGZNZVLVRQZVKAOUBWJARNWKAWIUNA
      VLBREVLPZUHUIUIZUJUKWEVKGIZASHZTZVLQZGWDVEZMZWEVKWNVTSHZTZVRQZGWDVEZMZVNW
      BWEWRXCWEWQXBGWDWEWNWDNZMZWPXAVLVRXFWPWNVKBSHZTZXAXEWEWPXHQWDAWOXGBWNVKWF
      WOPZEXGPZULUMWDBXGWTVTVKWNWGXJVTPZWTPZULUOWEWKXEWMUPUQURUSVLRNVNWSUTAJVFG
      WDVMAWORVKVLWFVMPZXIVAVBVRRNWBXDUTBJVFGWDWAVTWTRVKVRWDBVTXKWGUEWAPZXLVAVB
      VCVDVGVMABCVOVLVKDWLXMEWHVHVOBVTVJWAVRVKVJPVRPWHXKXNVHVCVI $.
      $( [4-Dec-2014] $)
  $}

  ${
    $d e m x y z R $.  $d e m x y z T $.  $d e m x y z U $.  $d x y z X $.
    $d x y z Y $.
    unitmulcl.1 $e |- U = ( Unit ` R ) $.
    unitmulcl.2 $e |- T = ( .r ` R ) $.
    $( The product of units is a unit.  (Contributed by Mario Carneiro,
       2-Dec-2014.) $)
    unitmulcl $p |- ( ( R e. Ring /\ X e. U /\ Y e. U ) ->
      ( X T Y ) e. U ) $=
      ( crg wcel co cfv cdsr wbr wa eqid isunit sylib syl wceq syl2anc sylanbrc
      w3a cur coppr simp3 simpld cbs simp1 rngidcl unitcl dvdsrmul2 mpd rngridm
      simp2 breqtrd dvdsrtr mp2and cmulr simprd opprbas opprrng opprmul rnglidm
      eqtr3d 3brtr4d ) AHIZDCIZECIZUBZDEBJZAUCKZALKZMZVJVKAUDKZLKZMZVJCIVIVJDVL
      MDVKVLMZVMVIVJDVKBJZDVLVIEVKVLMZVJVRVLMVIVSEVKVOMZVIVHVSVTNVFVGVHUEZVLAVN
      CVOVKEFVKOZVLOZVNOZVOOZPQZUFVIAUGKZVLABEVKDWGOZWCGVFVGVHUHZVIVFVKWGIZWIWG
      AVKWHWBUIRZVIVGDWGIZVFVGVHUNZWGACDWHFUJRZUKULVIVFWLVRDSWIWNWGABVKDWHGWBUM
      TUOVIVQDVKVOMZVIVGVQWONWMVLAVNCVOVKDFWBWCWDWEPQZUFVIWGVLAVKVJDWHWCWIWKUPU
      QVIVJEVOMVTVPVIEDVNURKZJZEVKWQJZVJEVOVIWOWRWSVOMVIVQWOWPUSVIWGVOVNWQDVKEW
      GAVNWDWHUTZWEWQOZVIVFVNHIWIAVNWDVARZWKVIVHEWGIZWAWGACEWHFUJRZUKULVIXCWLVJ
      WRSXDWNWGABWQVNEDWHGWDXAVBTVIVKEBJZEWSVIVFXCXEESWIXDWGABVKEWHGWBVCTVIXCWJ
      XEWSSXDWKWGABWQVNEVKWHGWDXAVBTVDVEVIVSVTWFUSVIWGVOVNVKVJEWTWEXBWKUPUQVLAV
      NCVOVKVJFWBWCWDWEPUA $.
      $( [1-Dec-2014] $)

    unitgrp.3 $e |- G =
           { <. ( Base ` ndx ) , U >. , <. ( +g ` ndx ) , T >. } $.
    $( The group of units is a group under multiplication.  (Contributed by
       Mario Carneiro, 2-Dec-2014.) $)
    unitgrp $p |- ( R e. Ring -> G e. Grp ) $=
      ( vx vy vm wcel cv co wceq wa wrex cfv eqid wbr syl2anc crg wral cgrp w3a
      vz unitmulcl 3adant3r3 cbs unitcl 3anim123i rngass sylan2 jca ralrimivvva
      ve cur 1unit rnglidm cdsr coppr simpr isunit sylib cmulr wb adantl dvdsr2
      cvv fvex sylancl opprbas anbi12d reeanv ad2antrr dvdsrmul opprmul simprrr
      simprl eqtrd breqtrd simpl simplr syl13anc oveq1d simprrl 3eqtr3d rngridm
      oveq2d breqtrrd sylanbrc ex reximdv2 rexlimdva sylbid mpd ralrimiva oveq1
      syl5bir eqeq1d eqeq2 rexbidv ralbidv rcla4ev cui eqeltri elgrp ) AUAKZHLZ
      ILZBMZCKZXJUELZBMXHXIXLBMBMNZOZUECUBICUBHCUBUOLZXHBMZXHNZJLZXHBMZXONZJCPZ
      OZHCUBZUOCPZDUCKXGXNHIUECCCXGXHCKZXICKZXLCKZUDZOXKXMXGYEYFXKYGABCXHXIEFUF
      UGYHXGXHAUHQZKZXIYIKZXLYIKZUDXMYEYJYFYKYGYLYIACXHYIRZEUIZYIACXIYMEUIYIACX
      LYMEUIUJYIABXHXIXLYMFUKULUMUNXGAUPQZCKYOXHBMZXHNZXSYONZJCPZOZHCUBZYDACYOE
      YORZUQXGYTHCXGYEOZYQYSYEXGYJYQYNYIABYOXHYMFUUBURULUUCXHYOAUSQZSZXHYOAUTQZ
      USQZSZOZYSUUCYEUUIXGYEVAUUDAUUFCUUGYOXHEUUBUUDRZUUFRZUUGRZVBVCUUCUUIXJYON
      ZIYIPZXHXRUUFVDQZMZYONZJYIPZOZYSUUCUUEUUNUUHUURUUCYJYOVHKZUUEUUNVEYEYJXGY
      NVFZAUPVIZIYIUUDABVHXHYOYMUUJFVGVJUUCYJUUTUUHUURVEUVAUVBJYIUUGUUFUUOVHXHY
      OYIAUUFUUKYMVKZUULUUORZVGVJVLUUSUUMUUQOZJYIPZIYIPUUCYSUUMUUQIJYIYIVMUUCUV
      FYSIYIUUCYKOZUVEYRJYICUVGXRYIKZUVEOZXRCKZYROUVGUVIOZUVJYRUVKXRYOUUDSXRYOU
      UGSUVJUVKXRXSYOUUDUVKUVHYJXRXSUUDSUVGUVHUVEVRZUUCYJYKUVIUVAVNZYIUUDABXRXH
      YMUUJFVOTUVKXSUUPYOUVKYJUVHXSUUPNUVMUVLYIABUUOUUFXHXRYMFUUKUVDVPTUVGUVHUU
      MUUQVQVSZVTUVKXRXRXHUUOMZYOUUGUVKUVHYJXRUVOUUGSUVLUVMYIUUGUUFUUOXRXHUVCUU
      LUVDVOTUVKXJXHXRBMZYOUVOUVKXIXRXHBUVKYOXIBMZXRYOBMZXIXRUVKXSXIBMZXRXJBMZU
      VQUVRUVKXGUVHYJYKUVSUVTNUUCXGYKUVIXGYEWAVNZUVLUVMUUCYKUVIWBZYIABXRXHXIYMF
      UKWCUVKXSYOXIBUVNWDUVKXJYOXRBUVGUVHUUMUUQWEZWHWFUVKXGYKUVQXINUWAUWBYIABYO
      XIYMFUUBURTUVKXGUVHUVRXRNUWAUVLYIABYOXRYMFUUBWGTWFWHUWCUVKUVHYJUVPUVONUVL
      UVMYIABUUOUUFXRXHYMFUUKUVDVPTWFWIUUDAUUFCUUGYOXREUUBUUJUUKUULVBWJUVNUMWKW
      LWMWRWNWOUMWPYCUUAUOYOCXOYONZYBYTHCUWDXQYQYAYSUWDXPYPXHXOYOXHBWQWSUWDXTYR
      JCXOYOXSWTXAVLXBXCTCBUOJDHIUECAXDQVHEAXDVIXEBAVDQVHFAVDVIXEGXFWJ $.
      $( [2-Dec-2014] $)

    unitgrp.4 $e |- O = ( 1r ` R ) $.
    $( The identity of the multiplicative group is ` 1r ` . $)
    unitgrpid $p |- ( R e. Ring -> O = ( 0g ` G ) ) $=
      ( wcel cfv wceq cbs eqid cvv cui fvex eqeltri ax-mp cmulr crg c0g rngidcl
      co rnglidm mpdan cgrp unitgrp 1unit grpbase cplusg grpplusg grpid syl2anc
      wb mpbid eqcomd ) AUAJZDUBKZEUREEBUDELZUSELZUREAMKZJUTVBAEVBNZIUCVBABEEVC
      GIUEUFURDUGJECJUTVAUOABCDFGHUHACEFIUICBDUSECOJCDMKLCAPKOFAPQRCBDOHUJSBOJB
      DUKKLBATKOGATQRCBDOHULSUSNUMUNUPUQ $.
      $( [2-Dec-2014] $)
  $}

  $c invr $.

  $( Extend class notation with multiplicative inverse. $)
  cinvr $a class invr $.

  $( Define multiplicative inverse. $)
  df-invr $a |- invr = ( r e. _V |-> ( invg `
      { <. ( Base ` ndx ) , ( Unit ` r ) >. ,
        <. ( +g ` ndx ) , ( .r ` r ) >. } ) ) $.

  ${
    $d r G $.  $d r R $.
    invrfval.u $e |- U = ( Unit ` R ) $.
    invrfval.t $e |- T = ( .r ` R ) $.
    invrfval.4 $e |- G =
           { <. ( Base ` ndx ) , U >. , <. ( +g ` ndx ) , T >. } $.
    invrfval.n $e |- N = ( invg ` G ) $.
    invrfval.i $e |- I = ( invr ` R ) $.
    $( Multiplicative inverse function for a division ring. $)
    invrfval $p |- ( R e. A -> I = N ) $=
      ( vr wcel cfv cminusg cui cop cmulr syl6eqr cinvr cvv wceq cnx cbs cplusg
      elex cv cpr fveq2 opeq2d preq12d fveq2d df-invr fvex fvmpt syl 3eqtr4g )
      BANZBUAOZEPOZFGUSBUBNUTVAUCBAUGMBUDUEOZMUHZQOZRZUDUFOZVCSOZRZUIZPOVAUBUAV
      CBUCZVIEPVJVIVBDRZVFCRZUIEVJVEVKVHVLVJVDDVBVJVDBQODVCBQUJHTUKVJVGCVFVJVGB
      SOCVCBSUJITUKULJTUMMUNEPUOUPUQLKUR $.
      $( [21-Sep-2011] $)
  $}

  ${
    unitinvcl.1 $e |- U = ( Unit ` R ) $.
    unitinvcl.2 $e |- I = ( invr ` R ) $.
    $( The inverse of a unit exists and is a unit.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    unitinvcl $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` X ) e. U ) $=
      ( crg wcel wa cfv cnx cbs cop cplusg cmulr cpr wceq eqid cvv cui invrfval
      cminusg adantr fveq1d cgrp unitgrp eqeltri grpbase ax-mp grpinvcl eqeltrd
      fvex sylan ) AGHZDBHZIZDCJDKLJBMKNJAOJZMPZUBJZJZBUPDCUSUNCUSQUOGAUQBURCUS
      EUQRZURRZUSRZFUAUCUDUNURUEHUOUTBHAUQBUREVAVBUFBURUSDBSHBURLJQBATJSEATULUG
      BUQURSVBUHUIVCUJUMUK $.
      $( [2-Dec-2014] $)

    $( The inverse of the inverse of a unit is the same element.  (Contributed
       by Mario Carneiro, 4-Dec-2014.) $)
    unitinvinv $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` ( I ` X ) ) = X ) $=
      ( crg wcel wa cfv cnx cbs cop cplusg cmulr cpr wceq eqid cvv cui invrfval
      cminusg adantr fveq1d fveq12d cgrp unitgrp fvex eqeltri grpbase grpinvinv
      ax-mp sylan eqtrd ) AGHZDBHZIZDCJZCJDKLJBMKNJAOJZMPZUBJZJZVAJZDUQURVBCVAU
      OCVAQUPGAUSBUTCVAEUSRZUTRZVARZFUAUCZUQDCVAVGUDUEUOUTUFHUPVCDQAUSBUTEVDVEU
      GBUTVADBSHBUTLJQBATJSEATUHUIBUSUTSVEUJULVFUKUMUN $.
      $( [4-Dec-2014] $)

    ${
      rnginvcl.3 $e |- B = ( Base ` R ) $.
      $( The inverse of a unit is an element of the ring.  (Contributed by
         Mario Carneiro, 2-Dec-2014.) $)
      rnginvcl $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` X ) e. B ) $=
        ( crg wcel wa cfv unitinvcl unitcl syl ) BIJECJKEDLZCJPAJBCDEFGMABCPHFN
        O $.
        $( [2-Dec-2014] $)
    $}

    unitinvcl.3 $e |- T = ( .r ` R ) $.
    unitinvcl.4 $e |- O = ( 1r ` R ) $.
    $( A unit times its inverse is the identity.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    unitlinv $p |- ( ( R e. Ring /\ X e. U ) -> ( ( I ` X ) T X ) = O ) $=
      ( crg wcel cnx cbs cfv cop cplusg wceq eqid cvv wa cpr cminusg co unitgrp
      c0g cgrp fvex eqeltri grpbase ax-mp cmulr grpplusg grplinv sylan invrfval
      cui adantr fveq1d oveq1d unitgrpid 3eqtr4d ) AKLZFCLZUAZFMNOCPMQOBPUBZUCO
      ZOZFBUDZVFUFOZFDOZFBUDEVCVFUGLVDVIVJRABCVFGIVFSZUECBVFVGVJFCTLCVFNORCAUQO
      TGAUQUHUICBVFTVLUJUKBTLBVFQORBAULOTIAULUHUICBVFTVLUMUKVJSVGSZUNUOVEVKVHFB
      VEFDVGVCDVGRVDKABCVFDVGGIVLVMHUPURUSUTVCEVJRVDABCVFEGIVLJVAURVB $.
      $( [2-Dec-2014] $)

    $( A unit times its inverse is the identity.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    unitrinv $p |- ( ( R e. Ring /\ X e. U ) -> ( X T ( I ` X ) ) = O ) $=
      ( crg wcel cnx cbs cfv cop cplusg wceq eqid cvv wa cpr cminusg co unitgrp
      c0g cgrp fvex eqeltri grpbase ax-mp cmulr grpplusg grprinv sylan invrfval
      cui adantr fveq1d oveq2d unitgrpid 3eqtr4d ) AKLZFCLZUAZFFMNOCPMQOBPUBZUC
      OZOZBUDZVFUFOZFFDOZBUDEVCVFUGLVDVIVJRABCVFGIVFSZUECBVFVGVJFCTLCVFNORCAUQO
      TGAUQUHUICBVFTVLUJUKBTLBVFQORBAULOTIAULUHUICBVFTVLUMUKVJSVGSZUNUOVEVKVHFB
      VEFDVGVCDVGRVDKABCVFDVGGIVLVMHUPURUSUTVCEVJRVDABCVFEGIVLJVAURVB $.
      $( [2-Dec-2014] $)
  $}

  ${
    0unit.1 $e |- U = ( Unit ` R ) $.
    0unit.2 $e |- Z = ( 0g ` R ) $.
    0unit.3 $e |- O = ( 1r ` R ) $.
    $( The additive identity is a unit if and only if ` 1 = 0 ` , i.e. we are
       in the zero ring.  (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    0unit $p |- ( R e. Ring -> ( Z e. U <-> O = Z ) ) $=
      ( crg wcel wceq wa cinvr cfv cmulr co eqid unitrinv cbs rnginvcl c0g idi
      rnglz syldan eqtr3d simpr 1unit adantr eqeltrrd impbida ) AHIZDBIZCDJZUJU
      KKDDALMZMZANMZOZCDAUOBUMCDEUMPZUOPZGQUJUKUNARMZIUPDJUSABUMDEUQUSPZSUSAUOU
      NDUTURDATMJFUAUBUCUDUJULKCDBUJULUEUJCBIULABCEGUFUGUHUI $.
      $( [4-Dec-2014] $)
  $}

  ${
    $d x N $.  $d x R $.  $d x X $.
    unitnegcl.1 $e |- U = ( Unit ` R ) $.
    unitnegcl.2 $e |- N = ( invg ` R ) $.
    $( The negative of a unit is a unit.  (Contributed by Mario Carneiro,
       4-Dec-2014.) $)
    unitnegcl $p |- ( ( R e. Ring /\ X e. U ) -> ( N ` X ) e. U ) $=
      ( crg wcel wa cfv cur cdsr wbr eqid adantr dvdsrneg breqtrd isunit mp2and
      dvdsrtr coppr cbs simpl cgrp rnggrp unitcl adantl grpinvcl wceq grpinvinv
      syl2anc syl2an simpr sylib simpld rngidcl opprbas opprrng simprd sylanbrc
      opprneg ) AGHZDBHZIZDCJZAKJZALJZMZVEVFAUAJZLJZMZVEBHVDVEDVGMDVFVGMZVHVDVE
      VECJZDVGVDAUBJZVGACVEVNNZVGNZVBVCUCZVDAUDHZDVNHZVEVNHVBVRVCAUEZOVCVSVBVNA
      BDVOEUFZUGVNACDVOFUHUKZFPVBVRVSVMDUIVCVTWAVNACDVOFUJULZQVDVLDVFVJMZVDVCVL
      WDIVBVCUMVGAVIBVJVFDEVFNZVPVINZVJNZRUNZUOVDVNVGAVFVEDVOVPVQVBVFVNHVCVNAVF
      VOWEUPOZTSVDVEDVJMWDVKVDVEVMDVJVDVNVJVICVEVNAVIWFVOUQZWGVBVIGHVCAVIWFUROZ
      WBACVIWFFVAPWCQVDVLWDWHUSVDVNVJVIVFVEDWJWGWKWITSVGAVIBVJVFVEEWEVPWFWGRUT
      $.
      $( [4-Dec-2014] $)
  $}

  $c /r $.

  $( Extend class notation with ring division. $)
  cdvr $a class /r $.

  ${
    $d r x y $.
    $( Define ring division. $)
    df-rngdv $a |- /r = ( r e. _V
     |-> ( x e. ( Base ` r ) , y e. ( Unit ` r )
         |-> ( x ( .r ` r ) ( ( invr ` r ) ` y ) ) ) ) $.
  $}

  ${
    $d r x y B $.  $d r x y I $.  $d r x y R $.  $d r x y T $.  $d r x y U $.
    $d x y X $.  $d y Y $.
    rngdvval.b $e |- B = ( Base ` R ) $.
    rngdvval.t $e |- T = ( .r ` R ) $.
    rngdvval.u $e |- U = ( Unit ` R ) $.
    rngdvval.i $e |- I = ( invr ` R ) $.
    rngdvval.d $e |- D = ( /r ` R ) $.
    $( Division operation in a ring.  (Revised by Mario Carneiro,
       2-Dec-2014.) $)
    rngdvfval $p |- ( R e. V ->
      D = ( x e. B , y e. U |-> ( x T ( I ` y ) ) ) ) $=
      ( cvv cfv cbs cui fveq2 syl6eqr vr wcel cv co cmpt2 wceq elex cinvr cmulr
      cdvr eqidd fveq1d oveq123d mpt2eq123dv df-rngdv fvex eqeltri mpt2ex fvmpt
      syl5eq syl ) EIUBEOUBZDABCGAUCZBUCZHPZFUDZUEZUFEIUGVBDEUJPVGNUAEABUAUCZQP
      ZVHRPZVCVDVHUHPZPZVHUIPZUDZUEVGOUJVHEUFZABVIVJVNCGVFVOVIEQPZCVHEQSJTVOVJE
      RPZGVHERSLTVOVCVCVLVEVMFVOVMEUIPFVHEUISKTVOVCUKVOVDVKHVOVKEUHPHVHEUHSMTUL
      UMUNABUAUOABCGVFCVPOJEQUPUQGVQOLERUPUQURUSUTVA $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)

    $( Division operation in a ring.  (Revised by Mario Carneiro,
       2-Dec-2014.) $)
    rngdvval $p |- ( ( X e. B /\ Y e. U ) -> ( X D Y ) = ( X T ( I ` Y ) ) ) $=
      ( vx vy wcel co cv cfv cbs wa cdm wceq elfvdm eleq2s adantr rngdvfval syl
      cmpt2 oveqd oveq1 fveq2 oveq2d eqid ovex ovmpt2 eqtrd ) GAPZHEPZUAZGHBQGH
      NOAENRZORZFSZDQZUIZQGHFSZDQZUTBVEGHUTCTUBZPZBVEUCURVIUSVIGCTSAGCTUDIUEUFN
      OABCDEFVHIJKLMUGUHUJNOGHAEVDVGVEGVCDQVAGVCDUKVBHUCVCVFGDVBHFULUMVEUNGVFDU
      OUPUQ $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)
  $}

  ${
    rngdvcl.b $e |- B = ( Base ` R ) $.
    rngdvcl.o $e |- U = ( Unit ` R ) $.
    rngdvcl.d $e |- D = ( /r ` R ) $.
    $( Closure of division operation. $)
    rngdvcl $p |- ( ( R e. Ring /\ X e. B /\ Y e. U ) -> ( X D Y ) e. B ) $=
      ( crg wcel w3a co cinvr cfv cmulr wceq eqid rngdvval 3adant1 rnginvcl
      3adant2 rngcl syld3an3 eqeltrd ) CJKZEAKZFDKZLEFBMZEFCNOZOZCPOZMZAUGUHUIU
      MQUFABCULDUJEFGULRZHUJRZISTUFUGUHUKAKZUMAKUFUHUPUGACDUJFHUOGUAUBACULEUKGU
      NUCUDUE $.
      $( [2-Jul-2014] $)

    $( The units are closed under division. $)
    unitdvcl $p |- ( ( R e. Ring /\ X e. U /\ Y e. U ) -> ( X D Y ) e. U ) $=
      ( crg wcel w3a co cinvr cfv cmulr wceq unitcl eqid rngdvval sylan 3adant1
      simp1 simp2 unitinvcl 3adant2 unitmulcl syl3anc eqeltrd ) CJKZEDKZFDKZLZE
      FBMZEFCNOZOZCPOZMZDUKULUNURQZUJUKEAKULUSACDEGHRABCUQDUOEFGUQSZHUOSZITUAUB
      UMUJUKUPDKZURDKUJUKULUCUJUKULUDUJULVBUKCDUOFHVAUEUFCUQDEUPHUTUGUHUI $.
      $( [2-Jul-2014] $)

    rngdvcan1.t $e |- T = ( .r ` R ) $.
    $( A cancellation law for division.  ( ~ divcan1 analog.) $)
    rngdvcan1 $p |- ( ( R e. Ring /\ X e. B /\ Y e. U ) ->
      ( ( X D Y ) T Y ) = X ) $=
      ( crg wcel w3a co cfv wceq eqid 3adant2 eqtrd cinvr rngdvval oveq1d simp1
      3adant1 simp2 rnginvcl unitcl 3ad2ant3 rngass syl13anc cur oveq2d rngridm
      unitlinv 3adant3 ) CLMZFAMZGEMZNZFGBOZGDOFGCUAPZPZDOZGDOZFUTVAVDGDURUSVAV
      DQUQABCDEVBFGHKIVBRZJUBUEUCUTVEFVCGDOZDOZFUTUQURVCAMZGAMZVEVHQUQURUSUDUQU
      RUSUFUQUSVIURACEVBGIVFHUGSUSUQVJURACEGHIUHUIACDFVCGHKUJUKUTVHFCULPZDOZFUT
      VGVKFDUQUSVGVKQURCDEVBVKGIVFKVKRZUOSUMUQURVLFQUSACDVKFHKVMUNUPTTT $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)

    $( A cancellation law for division.  ( ~ divcan3 analog.) $)
    rngdvcan3 $p |- ( ( R e. Ring /\ X e. B /\ Y e. U ) ->
      ( ( X T Y ) D Y ) = X ) $=
      ( crg wcel w3a co cfv wceq eqid 3adant2 eqtrd cinvr unitcl 3ad2ant3 rngcl
      syld3an3 simp3 rngdvval simp1 simp2 rnginvcl rngass syl13anc cur unitrinv
      syl2anc oveq2d rngridm 3adant3 ) CLMZFAMZGEMZNZFGDOZGBOZVCGCUAPZPZDOZFVBV
      CAMZVAVDVGQUSUTVAGAMZVHVAUSVIUTACEGHIUBUCZACDFGHKUDUEUSUTVAUFABCDEVEVCGHK
      IVERZJUGUOVBVGFGVFDOZDOZFVBUSUTVIVFAMZVGVMQUSUTVAUHUSUTVAUIVJUSVAVNUTACEV
      EGIVKHUJSACDFGVFHKUKULVBVMFCUMPZDOZFVBVLVOFDUSVAVLVOQUTCDEVEVOGIVKKVORZUN
      SUPUSUTVPFQVAACDVOFHKVQUQURTTT $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)

    $( An associative law for division.  ( ~ divass analog.) $)
    rngdvass $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. U ) ) ->
      ( ( X T Y ) D Z ) = ( X T ( Y D Z ) ) ) $=
      ( crg wcel w3a co cfv wceq syl2anc rngdvval wa cinvr simpr1 simpr2 simpr3
      simpl eqid rnginvcl rngass syl13anc rngcl 3adant3r3 oveq2d 3eqtr4d ) CMNZ
      FANZGANZHENZOZUAZFGDPZHCUBQZQZDPZFGVCDPZDPZVAHBPZFGHBPZDPUTUOUPUQVCANZVDV
      FRUOUSUFZUOUPUQURUCUOUPUQURUDZUTUOURVIVJUOUPUQURUEZACEVBHJVBUGZIUHSACDFGV
      CILUIUJUTVAANZURVGVDRUOUPUQVNURACDFGILUKULVLABCDEVBVAHILJVMKTSUTVHVEFDUTU
      QURVHVERVKVLABCDEVBGHILJVMKTSUMUN $.
      $( [4-Dec-2014] $)
  $}

  ${
    rnginvdv.b $e |- B = ( Base ` R ) $.
    rnginvdv.u $e |- U = ( Unit ` R ) $.
    rnginvdv.d $e |- D = ( /r ` R ) $.
    rnginvdv.o $e |- O = ( 1r ` R ) $.
    rnginvdv.i $e |- I = ( invr ` R ) $.
    $( Write the inverse function in terms of division. $)
    rnginvdv $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` X ) = ( O D X ) ) $=
      ( crg wcel wa co cfv cmulr wceq rngidcl eqid rngdvval sylan syldan eqtr2d
      rnginvcl rnglidm ) CMNZGDNZOFGBPZFGEQZCRQZPZUKUHFANUIUJUMSACFHKTABCULDEFG
      HULUAZILJUBUCUHUIUKANUMUKSACDEGILHUFACULFUKHUNKUGUDUE $.
      $( [2-Jul-2014] $)
  $}

