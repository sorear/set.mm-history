$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Divisibility
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c ||r $. $( Ring divides relation $)
  $c Unit $. $( Ring unit $)

  $( Ring divides relation. $)
  cdsr $a class ||r $.

  $( Ring unit. $)
  cui $a class Unit $.

  ${
    $d v w x y z $.
    $( Define the (left) divisibility relation in a ring.  Access to the right
       divisibility relation is available through
       ` ( ||r `` ( oppR `` R ) ) ` . $)
    df-dvdsr $a |- ||r = ( w e. _V |-> { <. x , y >. | ( x e. ( Base ` w ) /\
      E. z e. ( Base ` w ) ( x ( .r ` w ) z ) = y ) } ) $.

    $( Define the set of units in a ring. $)
    df-unit $a |- Unit = ( w e. _V |-> { x e. ( Base ` w ) |
      E. y e. ( Base ` w ) ( ( x ( .r ` w ) y ) = ( 1r ` w ) /\
        ( y ( .r ` w ) x ) = ( 1r ` w ) ) } ) $.
  $}

  ${
    $d b w x y z R $.
    reldvdsr.1 $e |- D = ( ||r ` R ) $.
    $( The divides relation is a relation.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    reldvdsr $p |- Rel D $=
      ( vx vw vz vy wrel cdsr cfv cv cbs wcel cmulr co wceq wrex cvv df-dvdsr
      wa relmptopab releqi mpbir ) AHBIJZHDKZEKZLJZMUEFKUFNJOGKPFUGQTEDGRBIDGFE
      SUAAUDCUBUC $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d x y D $.  $d r x y z R $.  $d r x y z T $.  $d x y z X $.  $d x y z Y $.
    $d b r x y z B $.  $d x y ph $.  $d x y Z $.
    dvdsr.1 $e |- B = ( Base ` R ) $.
    dvdsr.2 $e |- D = ( ||r ` R ) $.
    ${
      dvdsr.3 $e |- T = ( .r ` R ) $.
      $( Value of the divides relation.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      dvdsrval $p |- ( R e. V -> D = { <. x , y >. |
        ( x e. B /\ E. z e. B ( x T z ) = y ) } ) $=
        ( vr wcel cvv cv wceq wrex wa cfv cbs co copab elex cmulr fveq2 syl6eqr
        cdsr eleq2d rexeqdv anbi12d oveqd eqeq1d anbi2d bitrd opabbidv df-dvdsr
        rexbidv eqeltri cab eqcom rexbii abbii abrexex a1i opabex3 fvmpt syl5eq
        fvex syl ) FHMFNMZEAOZDMZVKCOZGUAZBOZPZCDQZRZABUBZPFHUCVJEFUGSVSJLFVKLO
        ZTSZMZVKVMVTUDSZUAZVOPZCWAQZRZABUBVSNUGVTFPZWGVRABWHWGVLWECDQZRVRWHWBVL
        WFWIWHWADVKWHWAFTSZDVTFTUEIUFZUHWHWECWADWKUIUJWHWIVQVLWHWEVPCDWHWDVNVOW
        HWCGVKVMWHWCFUDSGVTFUDUEKUFUKULUQUMUNUOABCLUPVQABDDWJNIFTVHURZVQBUSZNMV
        LWMVOVNPZCDQZBUSNVQWOBVPWNCDVNVOUTVAVBCBDVNWLVCURVDVEVFVGVI $.
        $( [1-Dec-2014] $)

      $( Value of the divides relation.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      dvdsr $p |- ( ( R e. V /\ Y e. W ) -> ( X D Y <->
        ( X e. B /\ E. z e. B ( X T z ) = Y ) ) ) $=
        ( vx vy wcel wa cvv wbr cv wceq co wrex wi reldvdsr brrelexi a1i adantr
        elex wb copab dvdsrval breqd simpl eleq1d simpr eqeq12d rexbidv anbi12d
        oveq1d eqid brabga ancoms sylan9bb expr pm5.21ndd ) DFOZIGOZPZHQOZHICRZ
        HBOZHASZEUAZITZABUBZPZVJVIUCVHHICCDKUDUEUFVPVIUCVHVKVIVOHBUHUGUFVFVGVIV
        JVPUIVFVJHIMSZBOZVQVLEUAZNSZTZABUBZPZMNUJZRZVGVIPVPVFCWDHIMNABCDEFJKLUK
        ULVIVGWEVPUIWCVPMNHIWDQGVQHTZVTITZPZVRVKWBVOWHVQHBWFWGUMZUNWHWAVNABWHVS
        VMVTIWHVQHVLEWIUSWFWGUOUPUQURWDUTVAVBVCVDVE $.
        $( [1-Dec-2014] $)

      $( Value of the divides relation.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      dvdsr2 $p |- ( ( R e. V /\ X e. B /\ Y e. W ) ->
                     ( X D Y <-> E. z e. B ( X T z ) = Y ) ) $=
        ( wcel w3a wbr cv co wceq wrex wa dvdsr 3adant2 simp2 biantrurd bitr4d
        wb ) DFMZHBMZIGMZNZHICOZUHHAPEQIRABSZTZULUGUIUKUMUFUHABCDEFGHIJKLUAUBUJ
        UHULUGUHUIUCUDUE $.
        $( [1-Dec-2014] $)

      $( A right-multiple of ` X ` is divisible by ` X ` .  (Contributed by
         Mario Carneiro, 1-Dec-2014.) $)
      dvdsrmul $p |- ( ( R e. V /\ X e. B /\ Y e. B ) -> X D ( X T Y ) ) $=
        ( vz wcel w3a co wbr cv wceq wrex cvv sylancl wa simp1 ovex dvdsr simp2
        wb simp3 eqid oveq2 eqeq1d rcla4ev mpbir2and ) CELZFALZGALZMZFFGDNZBOZU
        NFKPZDNZUQQZKARZUPUMUQSLURUNVBUAUFUMUNUOUBFGDUCKABCDESFUQHIJUDTUMUNUOUE
        UPUOUQUQQZVBUMUNUOUGUQUHVAVCKGAUSGQUTUQUQUSGFDUIUJUKTUL $.
        $( [1-Dec-2014] $)

      dvdsrmul2.4 $e |- ( ph -> R e. Ring ) $.
      dvdsrmul2.5 $e |- ( ph -> Y e. B ) $.
      dvdsrmul2.6 $e |- ( ph -> Z e. B ) $.
      $( The divisibility relation is preserved under left-multiplication.
         (Contributed by Mario Carneiro, 1-Dec-2014.) $)
      dvdsrmul2 $p |- ( ph -> ( X D Y -> ( Z T X ) D ( Z T Y ) ) ) $=
        ( vx wcel co wceq wa adantr wbr cv wrex wb dvdsr syl2anc simpr syl13anc
        crg rngass oveq2 eqeq2d syl5ibcom reximdva cvv rngcl syl3anc a1i dvdsr2
        ovex sylibrd expimpd sylbid ) AFGCUAZFBPZFOUBZEQZGRZOBUCZSZHFEQZHGEQZCU
        AZADUIPZGBPVDVJUDLMOBCDEUIBFGIJKUEUFAVEVIVMAVESZVIVKVFEQZVLRZOBUCZVMVOV
        HVQOBVOVFBPZSZVPHVGEQZRZVHVQVTVNHBPZVEVSWBVOVNVSAVNVELTZTVOWCVSAWCVENTZ
        TVOVEVSAVEUGZTVOVSUGBDEHFVFIKUJUHVHWAVLVPVGGHEUKULUMUNVOVNVKBPZVLUOPZVM
        VRUDWDVOVNWCVEWGWDWEWFBDEHFIKUPUQWHVOHGEUTUROBCDEUIUOVKVLIJKUSUQVAVBVC
        $.
        $( [1-Dec-2014] $)
    $}

    dvdsrid.3 $e |- ( ph -> R e. Ring ) $.
    dvdsrid.4 $e |- ( ph -> X e. B ) $.
    $( An element in a (unital) ring divides itself.  (Contributed by Mario
       Carneiro, 1-Dec-2014.) $)
    dvdsrid $p |- ( ph -> X D X ) $=
      ( cur cfv cmulr co crg wcel wbr eqid rngidcl syl dvdsrmul syl3anc rngridm
      wceq syl2anc breqtrd ) AEEDJKZDLKZMZECADNOZEBOZUFBOZEUHCPHIAUIUKHBDUFFUFQ
      ZRSBCDUGNEUFFGUGQZTUAAUIUJUHEUCHIBDUGUFEFUMULUBUDUE $.
      $( [1-Dec-2014] $)

    $( Divisibility is transitive.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    dvdsrtr $p |- ( ph -> ( ( Y D Z /\ Z D X ) -> Y D X ) ) $=
      ( vx vy wbr wa wcel co wceq wrex crg cv cmulr cfv wb dvdsr syl2anc anbi1d
      eqid sylan adantrr pm5.32da bitrd wi reeanv ad2antrr simplr simprr simprl
      an4 rngcl syl3anc dvdsrmul rngass syl13anc breqtrrd oveq1 breq2 sylan9bbr
      breq2d syl5ibcom rexlimdvva syl5bir adantrl expimpd sylbid ancomsd ) AGEC
      NZFGCNZFECNZAVQVROZGBPZGLUAZDUBUCZQZERZLBSZOZFBPZFMUAZWCQZGRZMBSZOZOZVSAV
      TWGVROWNAVQWGVRADTPZEBPVQWGUDJKLBCDWCTBGEHIWCUHZUEUFUGAWGVRWMAWAVRWMUDZWF
      AWOWAWQJMBCDWCTBFGHIWPUEUIUJUKULWNWAWHOZWFWLOZOAVSWAWHWFWLUSAWRWSVSAWHWSV
      SUMWAWSWEWKOZMBSLBSAWHOZVSWEWKLMBBUNXAWTVSLMBBXAWBBPZWIBPZOZOZFWJWBWCQZCN
      ZWTVSXEFFWIWBWCQZWCQZXFCXEWOWHXHBPZFXICNAWOWHXDJUOZAWHXDUPZXEWOXCXBXJXKXA
      XBXCUQZXAXBXCURZBDWCWIWBHWPUTVABCDWCTFXHHIWPVBVAXEWOWHXCXBXFXIRXKXLXMXNBD
      WCFWIWBHWPVCVDVEWKXGFWDCNWEVSWKXFWDFCWJGWBWCVFVIWDEFCVGVHVJVKVLVMVNVLVOVP
      $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d w x y B $.  $d w x O $.  $d w x y R $.  $d w x T $.  $d x y X $.
    unit.1 $e |- U = ( Unit ` R ) $.
    unit.2 $e |- O = ( 1r ` R ) $.
    ${
      unit.3 $e |- B = ( Base ` R ) $.
      unit.4 $e |- T = ( .r ` R ) $.
      $( Value of the set of units.  (Contributed by Mario Carneiro,
         1-Dec-2014.) $)
      unitval $p |- ( R e. V -> U = { x e. B |
        E. y e. B ( ( x T y ) = O /\ ( y T x ) = O ) } ) $=
        ( vw cvv cv co wceq cfv cmulr cbs wcel wrex crab elex cui fveq2 syl6eqr
        wa oveqd eqeq12d anbi12d rexeqbidv rabeqbidv df-unit fvex eqeltri rabex
        cur fvmpt syl5eq syl ) DHUADNUAZFAOZBOZEPZGQZVDVCEPZGQZUHZBCUBZACUCZQDH
        UDVBFDUERVKIMDVCVDMOZSRZPZVLURRZQZVDVCVMPZVOQZUHZBVLTRZUBZAVTUCVKNUEVLD
        QZWAVJAVTCWBVTDTRZCVLDTUFKUGZWBVSVIBVTCWDWBVPVFVRVHWBVNVEVOGWBVMEVCVDWB
        VMDSREVLDSUFLUGZUIWBVODURRGVLDURUFJUGZUJWBVQVGVOGWBVMEVDVCWEUIWFUJUKULU
        MABMUNVJACCWCNKDTUOUPUQUSUTVA $.
        $( [1-Dec-2014] $)

      $( Property of being a unit of a ring.  A unit is an element that has a
         two-sided inverse.  (Contributed by Mario Carneiro, 1-Dec-2014.) $)
      isunit $p |- ( R e. V -> ( X e. U <-> ( X e. B /\
        E. y e. B ( ( X T y ) = O /\ ( y T X ) = O ) ) ) ) $=
        ( vx wcel cv co wceq wa wrex eqeq1d crab unitval eleq2d anbi12d rexbidv
        oveq1 oveq2 elrab syl6bb ) CGNZHENHMOZAOZDPZFQZULUKDPZFQZRZABSZMBUAZNHB
        NHULDPZFQZULHDPZFQZRZABSZRUJEUSHMABCDEFGIJKLUBUCURVEMHBUKHQZUQVDABVFUNV
        AUPVCVFUMUTFUKHULDUFTVFUOVBFUKHULDUGTUDUEUHUI $.
        $( [1-Dec-2014] $)
    $}

    $( The multiplicative identity is a unit.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    1unit $p |- ( R e. Ring -> O e. U ) $=
      ( vx crg wcel cbs cfv cv cmulr co wceq wa wrex eqid isunit rngidcl eqeq1d
      rnglidm mpdan oveq2 oveq1 anbi12d rcla4ev syl12anc mpbir2and ) AGHZCBHCAI
      JZHZCFKZALJZMZCNZULCUMMZCNZOZFUJPZFUJAUMBCGCDEUJQZUMQZRUJACUTESZUIUKCCUMM
      ZCNZVDUSVBUIUKVDVBUJAUMCCUTVAEUAUBZVEURVDVDOFCUJULCNZUOVDUQVDVFUNVCCULCCU
      MUCTVFUPVCCULCCUMUDTUEUFUGUH $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d x B $.  $d x R $.  $d x X $.
    unitcl.1 $e |- B = ( Base ` R ) $.
    unitcl.2 $e |- U = ( Unit ` R ) $.
    $( A unit is an element of the base set.  (Contributed by Mario Carneiro,
       1-Dec-2014.) $)
    unitcl $p |- ( ( R e. V /\ X e. U ) -> X e. B ) $=
      ( vx wcel wa cv cmulr cfv co cur wceq wrex eqid isunit biimpa simpld ) BD
      IZECIZJEAIZEHKZBLMZNBOMZPUEEUFNUGPJHAQZUBUCUDUHJHABUFCUGDEGUGRFUFRSTUA $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d e m x y z R $.  $d e m x y z T $.  $d e m x y z U $.  $d x y z X $.
    $d x y z Y $.
    unitmulcl.1 $e |- U = ( Unit ` R ) $.
    unitmulcl.2 $e |- T = ( .r ` R ) $.
    $( The product of units is a unit.  (Contributed by Mario Carneiro,
       2-Dec-2014.) $)
    unitmulcl $p |- ( ( R e. Ring /\ X e. U /\ Y e. U ) ->
      ( X T Y ) e. U ) $=
      ( vx vy vz crg wcel co cv wceq wa wrex rngass syl13anc isunit cbs cfv cur
      an4 reeanv wi simpl1 simpl2l simpl2r eqid syl3anc simpl3r simpl3l simprrl
      rngcl oveq1d rnglidm syl2anc 3eqtr3d oveq2d simprll simprlr simprrr oveq2
      w3a 3eqtrd eqeq1d oveq1 anbi12d rcla4ev syl12anc 3expa rexlimdvva syl5bir
      jca ex expimpd 3imtr4d 3impib ) AKLZDCLZECLZDEBMZCLZVTDAUAUBZLZDHNZBMZAUC
      UBZOZWGDBMZWIOZPZHWEQZPZEWELZEINZBMZWIOZWQEBMZWIOZPZIWEQZPZPZWCWELZWCJNZB
      MZWIOZXGWCBMZWIOZPZJWEQZPZWAWBPWDXEWFWPPZWNXCPZPVTXNWFWPWNXCUDVTXOXPXNXPW
      MXBPZIWEQHWEQVTXOPZXNWMXBHIWEWEUEXRXQXNHIWEWEVTXOWGWELZWQWELZPZXQXNUFVTXO
      YAVEZXQXNYBXQPZXFXMYCVTWFWPXFVTXOYAXQUGZWFWPVTYAXQUHZWFWPVTYAXQUIZWEABDEW
      EUJZGUOUKZYCWQWGBMZWELZWCYIBMZWIOZYIWCBMZWIOZXMYCVTXTXSYJYDXSXTVTXOXQULZX
      SXTVTXOXQUMZWEABWQWGYGGUOUKZYCYKDEYIBMZBMZWHWIYCVTWFWPYJYKYSOYDYEYFYQWEAB
      DEYIYGGRSYCYRWGDBYCWRWGBMZWIWGBMZYRWGYCWRWIWGBYBWMWSXAUNUPYCVTWPXTXSYTYRO
      YDYFYOYPWEABEWQWGYGGRSYCVTXSUUAWGOYDYPWEABWIWGYGGWIUJZUQURUSUTYBWJWLXBVAV
      FYCYMWQWGWCBMZBMZWTWIYCVTXTXSXFYMUUDOYDYOYPYHWEABWQWGWCYGGRSYCUUCEWQBYCWK
      EBMZWIEBMZUUCEYCWKWIEBYBWJWLXBVBUPYCVTXSWFWPUUEUUCOYDYPYEYFWEABWGDEYGGRSY
      CVTWPUUFEOYDYFWEABWIEYGGUUBUQURUSUTYBWMWSXAVCVFXLYLYNPJYIWEXGYIOZXIYLXKYN
      UUGXHYKWIXGYIWCBVDVGUUGXJYMWIXGYIWCBVHVGVIVJVKVOVPVLVMVNVQVNVTWAWOWBXDHWE
      ABCWIKDFUUBYGGTIWEABCWIKEFUUBYGGTVIJWEABCWIKWCFUUBYGGTVRVS $.
      $( [1-Dec-2014] $)

    unitgrp.3 $e |- G =
           { <. ( Base ` ndx ) , U >. , <. ( +g ` ndx ) , T >. } $.
    $( The group of units is a group under multiplication.  (Contributed by
       Mario Carneiro, 2-Dec-2014.) $)
    unitgrp $p |- ( R e. Ring -> G e. Grp ) $=
      ( vx vy vz ve vm crg wcel cv co wceq wa wral wrex w3a unitmulcl 3adant3r3
      cgrp cbs cfv eqid unitcl ex 3anim123d imp rngass syldan ralrimivvva 1unit
      jca cur rnglidm isunit biimpa simprd ad2antrr simprl adantr simprrr oveq2
      wb simprrl eqeq1d oveq1 anbi12d syl12anc mpbir2and reximdv2 mpd ralrimiva
      rcla4ev eqeq2 rexbidv ralbidv syl2anc cui cvv fvex eqeltri cmulr sylanbrc
      elgrp ) AMNZHOZIOZBPZCNZWLJOZBPWJWKWNBPBPQZRZJCSICSHCSKOZWJBPZWJQZLOZWJBP
      ZWQQZLCTZRZHCSZKCTZDUDNWIWPHIJCCCWIWJCNZWKCNZWNCNZUAZRWMWOWIXGXHWMXIABCWJ
      WKEFUBUCWIXJWJAUEUFZNZWKXKNZWNXKNZUAZWOWIXJXOWIXGXLXHXMXIXNWIXGXLXKACMWJX
      KUGZEUHZUIWIXHXMXKACMWKXPEUHUIWIXIXNXKACMWNXPEUHUIUJUKXKABWJWKWNXPFULUMUP
      UNWIAUQUFZCNXRWJBPZWJQZXAXRQZLCTZRZHCSZXFACXREXRUGZUOWIYCHCWIXGRZXTYBWIXG
      XLXTXQXKABXRWJXPFYEURUMYFWJWTBPZXRQZYARZLXKTZYBYFXLYJWIXGXLYJRLXKABCXRMWJ
      EYEXPFUSUTVAYFYIYALXKCYFWTXKNZYIRZWTCNZYARYFYLRZYMYAYNYMYKWTWKBPZXRQZWKWT
      BPZXRQZRZIXKTZWIYMYKYTRVGXGYLIXKABCXRMWTEYEXPFUSVBYFYKYIVCYNXLYAYHYTYFXLY
      LXQVDYFYKYHYAVEZYFYKYHYAVHYSYAYHRIWJXKWKWJQZYPYAYRYHUUBYOXAXRWKWJWTBVFVIU
      UBYQYGXRWKWJWTBVJVIVKVQVLVMUUAUPUIVNVOUPVPXEYDKXRCWQXRQZXDYCHCUUCWSXTXCYB
      UUCWRXSWJWQXRWJBVJVIUUCXBYALCWQXRXAVRVSVKVTVQWACBKLDHIJCAWBUFWCEAWBWDWEBA
      WFUFWCFAWFWDWEGWHWG $.
      $( [2-Dec-2014] $)

    unitgrp.4 $e |- O = ( 1r ` R ) $.
    $( The identity of the multiplicative group is ` 1r ` . $)
    unitgrpid $p |- ( R e. Ring -> O = ( 0g ` G ) ) $=
      ( wcel cfv wceq cbs eqid cvv cui fvex eqeltri ax-mp cmulr crg c0g rngidcl
      co rnglidm mpdan cgrp unitgrp 1unit grpbase cplusg grpplusg grpid syl2anc
      wb mpbid eqcomd ) AUAJZDUBKZEUREEBUDELZUSELZUREAMKZJUTVBAEVBNZIUCVBABEEVC
      GIUEUFURDUGJECJUTVAUOABCDFGHUHACEFIUICBDUSECOJCDMKLCAPKOFAPQRCBDOHUJSBOJB
      DUKKLBATKOGATQRCBDOHULSUSNUMUNUPUQ $.
      $( [2-Dec-2014] $)
  $}

  $c invr $.

  $( Extend class notation with multiplicative inverse. $)
  cinvr $a class invr $.

  $( Define multiplicative inverse. $)
  df-invr $a |- invr = ( r e. _V |-> ( invg `
      { <. ( Base ` ndx ) , ( Unit ` r ) >. ,
        <. ( +g ` ndx ) , ( .r ` r ) >. } ) ) $.

  ${
    $d r G $.  $d r R $.
    invrfval.u $e |- U = ( Unit ` R ) $.
    invrfval.t $e |- T = ( .r ` R ) $.
    invrfval.4 $e |- G =
           { <. ( Base ` ndx ) , U >. , <. ( +g ` ndx ) , T >. } $.
    invrfval.n $e |- N = ( invg ` G ) $.
    invrfval.i $e |- I = ( invr ` R ) $.
    $( Multiplicative inverse function for a division ring. $)
    invrfval $p |- ( R e. A -> I = N ) $=
      ( vr wcel cfv cminusg cui cop cmulr syl6eqr cinvr cvv wceq cnx cbs cplusg
      elex cv cpr fveq2 opeq2d preq12d fveq2d df-invr fvex fvmpt syl 3eqtr4g )
      BANZBUAOZEPOZFGUSBUBNUTVAUCBAUGMBUDUEOZMUHZQOZRZUDUFOZVCSOZRZUIZPOVAUBUAV
      CBUCZVIEPVJVIVBDRZVFCRZUIEVJVEVKVHVLVJVDDVBVJVDBQODVCBQUJHTUKVJVGCVFVJVGB
      SOCVCBSUJITUKULJTUMMUNEPUOUPUQLKUR $.
      $( [21-Sep-2011] $)
  $}

  ${
    unitinvcl.1 $e |- U = ( Unit ` R ) $.
    unitinvcl.2 $e |- I = ( invr ` R ) $.
    $( The inverse of a unit exists and is a unit.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    unitinvcl $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` X ) e. U ) $=
      ( crg wcel wa cfv cnx cbs cop cplusg cmulr cpr wceq eqid cvv cui invrfval
      cminusg adantr fveq1d cgrp unitgrp eqeltri grpbase ax-mp grpinvcl eqeltrd
      fvex sylan ) AGHZDBHZIZDCJDKLJBMKNJAOJZMPZUBJZJZBUPDCUSUNCUSQUOGAUQBURCUS
      EUQRZURRZUSRZFUAUCUDUNURUEHUOUTBHAUQBUREVAVBUFBURUSDBSHBURLJQBATJSEATULUG
      BUQURSVBUHUIVCUJUMUK $.
      $( [2-Dec-2014] $)

    ${
      rnginvcl.3 $e |- B = ( Base ` R ) $.
      $( The inverse of a unit is an element of the ring.  (Contributed by
         Mario Carneiro, 2-Dec-2014.) $)
      rnginvcl $p |- ( ( R e. Ring /\ X e. U ) -> ( I ` X ) e. B ) $=
        ( crg wcel cfv unitinvcl unitcl syldan ) BIJECJEDKZCJOAJBCDEFGLABCIOHFM
        N $.
        $( [2-Dec-2014] $)
    $}

    unitinvcl.3 $e |- T = ( .r ` R ) $.
    unitinvcl.4 $e |- O = ( 1r ` R ) $.
    $( A unit times its inverse is the identity. $)
    unitlinv $p |- ( ( R e. Ring /\ X e. U ) -> ( ( I ` X ) T X ) = O ) $=
      ( crg wcel cnx cbs cfv cop cplusg wceq eqid cvv wa cpr cminusg co unitgrp
      c0g cgrp fvex eqeltri grpbase ax-mp cmulr grpplusg grplinv sylan invrfval
      cui adantr fveq1d oveq1d unitgrpid 3eqtr4d ) AKLZFCLZUAZFMNOCPMQOBPUBZUCO
      ZOZFBUDZVFUFOZFDOZFBUDEVCVFUGLVDVIVJRABCVFGIVFSZUECBVFVGVJFCTLCVFNORCAUQO
      TGAUQUHUICBVFTVLUJUKBTLBVFQORBAULOTIAULUHUICBVFTVLUMUKVJSVGSZUNUOVEVKVHFB
      VEFDVGVCDVGRVDKABCVFDVGGIVLVMHUPURUSUTVCEVJRVDABCVFEGIVLJVAURVB $.
      $( [1-Dec-2014] $)

    $( A unit times its inverse is the identity. $)
    unitrinv $p |- ( ( R e. Ring /\ X e. U ) -> ( X T ( I ` X ) ) = O ) $=
      ( crg wcel cnx cbs cfv cop cplusg wceq eqid cvv wa cpr cminusg co unitgrp
      c0g cgrp fvex eqeltri grpbase ax-mp cmulr grpplusg grprinv sylan invrfval
      cui adantr fveq1d oveq2d unitgrpid 3eqtr4d ) AKLZFCLZUAZFFMNOCPMQOBPUBZUC
      OZOZBUDZVFUFOZFFDOZBUDEVCVFUGLVDVIVJRABCVFGIVFSZUECBVFVGVJFCTLCVFNORCAUQO
      TGAUQUHUICBVFTVLUJUKBTLBVFQORBAULOTIAULUHUICBVFTVLUMUKVJSVGSZUNUOVEVKVHFB
      VEFDVGVCDVGRVDKABCVFDVGGIVLVMHUPURUSUTVCEVJRVDABCVFEGIVLJVAURVB $.
      $( [1-Dec-2014] $)
  $}

  $c /r $.

  $( Extend class notation with ring division. $)
  cdvr $a class /r $.

  ${
    $d r x y $.
    $( Define ring division. $)
    df-rngdv $a |- /r = ( r e. _V
     |-> ( x e. ( Base ` r ) , y e. ( Unit ` r )
         |-> ( x ( .r ` r ) ( ( invr ` r ) ` y ) ) ) ) $.
  $}

  ${
    $d r x y B $.  $d r x y I $.  $d r x y R $.  $d r x y T $.  $d r x y U $.
    $d x y X $.  $d y Y $.
    rngdvval.b $e |- B = ( Base ` R ) $.
    rngdvval.t $e |- T = ( .r ` R ) $.
    rngdvval.u $e |- U = ( Unit ` R ) $.
    rngdvval.i $e |- I = ( invr ` R ) $.
    rngdvval.d $e |- D = ( /r ` R ) $.
    rngdvval.r $e |- ( ph -> R e. V ) $.
    $( Division operation for a division ring.  (Revised by Mario Carneiro,
       2-Dec-2014.) $)
    rngdvfval $p |- ( ph -> D = ( x e. B , y e. U |-> ( x T ( I ` y ) ) ) ) $=
      ( cvv cfv cbs cui vr wcel cv co cmpt2 wceq elex cinvr cmulr fveq2 syl6eqr
      cdvr eqidd fveq1d oveq123d mpt2eq123dv df-rngdv fvex eqeltri mpt2ex fvmpt
      syl5eq 3syl ) AFJUBFQUBZEBCDHBUCZCUCZIRZGUDZUEZUFPFJUGVDEFULRVIOUAFBCUAUC
      ZSRZVJTRZVEVFVJUHRZRZVJUIRZUDZUEVIQULVJFUFZBCVKVLVPDHVHVQVKFSRZDVJFSUJKUK
      VQVLFTRZHVJFTUJMUKVQVEVEVNVGVOGVQVOFUIRGVJFUIUJLUKVQVEUMVQVFVMIVQVMFUHRIV
      JFUHUJNUKUNUOUPBCUAUQBCDHVHDVRQKFSURUSHVSQMFTURUSUTVAVBVC $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)

    rngdvval.x $e |- ( ph -> X e. B ) $.
    rngdvval.y $e |- ( ph -> Y e. U ) $.
    $( Division operation for a division ring.  (Revised by Mario Carneiro,
       2-Dec-2014.) $)
    rngdvval $p |- ( ph -> ( X D Y ) = ( X T ( I ` Y ) ) ) $=
      ( vx co vy cv cfv cmpt2 rngdvfval oveqd wcel wceq oveq1 fveq2 oveq2d eqid
      ovex ovmpt2 syl2anc eqtrd ) AIJCTIJSUABFSUBZUAUBZGUCZETZUDZTZIJGUCZETZACV
      AIJASUABCDEFGHKLMNOPUEUFAIBUGJFUGVBVDUHQRSUAIJBFUTVDVAIUSETUQIUSEUIURJUHU
      SVCIEURJGUJUKVAULIVCEUMUNUOUP $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)
  $}

  ${
    rngdvcl.b $e |- B = ( Base ` R ) $.
    rngdvcl.o $e |- U = ( Unit ` R ) $.
    rngdvcl.d $e |- D = ( /r ` R ) $.
    rngdvcl.r $e |- ( ph -> R e. Ring ) $.
    rngdvcl.x $e |- ( ph -> X e. B ) $.
    rngdvcl.y $e |- ( ph -> Y e. U ) $.
    $( Closure of division operation. $)
    rngdvcl $p |- ( ph -> ( X D Y ) e. B ) $=
      ( co cinvr cfv cmulr crg eqid wcel rngdvval syl2anc rngcl syl3anc eqeltrd
      rnginvcl ) AFGCNFGDOPZPZDQPZNZBABCDUIEUGRFGHUISZIUGSZJKLMUAADRTZFBTUHBTZU
      JBTKLAUMGETUNKMBDEUGGIULHUFUBBDUIFUHHUKUCUDUE $.
      $( [2-Jul-2014] $)

    rngdvcan1.t $e |- T = ( .r ` R ) $.
    $( A cancellation law for division.  ( ~ divcan1 analog.) $)
    rngdvcan1 $p |- ( ph -> ( ( X D Y ) T Y ) = X ) $=
      ( co cfv crg wcel syl2anc cinvr eqid rngdvval oveq1d wceq rnginvcl unitcl
      rngass syl13anc cur unitlinv oveq2d rngridm eqtrd ) AGHCPZHEPGHDUAQZQZEPZ
      HEPZGAUOURHEABCDEFUPRGHIOJUPUBZKLMNUCUDAUSGUQHEPZEPZGADRSZGBSZUQBSZHBSZUS
      VBUELMAVCHFSZVELNBDFUPHJUTIUFTAVCVGVFLNBDFRHIJUGTBDEGUQHIOUHUIAVBGDUJQZEP
      ZGAVAVHGEAVCVGVAVHUELNDEFUPVHHJUTOVHUBZUKTULAVCVDVIGUELMBDEVHGIOVJUMTUNUN
      UN $.
      $( [2-Dec-2014] $) $( [2-Jul-2014] $)
  $}

