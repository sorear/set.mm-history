$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Absolute value (abstract algebra)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c AbsVal $. $( Absolute value function $)

  $( The set of absolute values on a ring. $)
  cabv $a class AbsVal $.

  ${
    $d f r x y $.
    $( Define the set of absolute values on a ring.  An absolute value is a
       generalization of the usual absolute value function ~ df-abs to
       arbitrary rings. $)
    df-abv $a |- AbsVal = ( r e. Ring |->
                            { f e. ( ( 0 [,) +oo ) ^m ( Base ` r ) ) |
      A. x e. ( Base ` r ) ( ( ( f ` x ) = 0 <-> x = ( 0g ` r ) ) /\
      A. y e. ( Base ` r )
        ( ( f ` ( x ( .r ` r ) y ) ) = ( ( f ` x ) x. ( f ` y ) ) /\
          ( f ` ( x ( +g ` r ) y ) ) <_ ( ( f ` x ) + ( f ` y ) ) ) ) } ) $.
  $}

  ${
    $d f r x y B $.  $d f x y F $.  $d f r P $.  $d x y ph $.  $d f r x y R $.
    $d f r T $.  $d f r Z $.
    abvfval.a $e |- A = ( AbsVal ` R ) $.
    abvfval.b $e |- B = ( Base ` R ) $.
    abvfval.p $e |- P = ( +g ` R ) $.
    abvfval.t $e |- T = ( .r ` R ) $.
    abvfval.z $e |- Z = ( 0g ` R ) $.
    $( Value of the set of absolute values.  (Contributed by Mario Carneiro,
       8-Sep-2014.) $)
    abvfval $p |- ( R e. Ring -> A = { f e. ( ( 0 [,) +oo ) ^m B ) |
    A. x e. B ( ( ( f ` x ) = 0 <-> x = Z ) /\ A. y e. B
      ( ( f ` ( x T y ) ) = ( ( f ` x ) x. ( f ` y ) ) /\
        ( f ` ( x P y ) ) <_ ( ( f ` x ) + ( f ` y ) ) ) ) } ) $=
      ( cfv cv wceq co wa wral vr crg wcel cabv cc0 wb cmul caddc cle cpnf cico
      wbr cmap c0g cmulr cplusg fveq2 syl6eqr oveq2d eqeq2d bibi2d oveqd fveq2d
      crab cbs eqeq1d breq1d raleqbidv rabeqbidv df-abv ovex rabex fvmpt syl5eq
      anbi12d ) FUBUCCFUDOAPZHPZOZUEQZVPIQZUFZVPBPZGRZVQOZVRWBVQOZUGRZQZVPWBERZ
      VQOZVRWEUHRZUIULZSZBDTZSZADTZHUEUJUKRZDUMRZVDZJUAFVSVPUAPZUNOZQZUFZVPWBWS
      UOOZRZVQOZWFQZVPWBWSUPOZRZVQOZWJUIULZSZBWSVEOZTZSZAXLTZHWPXLUMRZVDWRUBUDW
      SFQZXOWOHXPWQXQXLDWPUMXQXLFVEODWSFVEUQKURZUSXQXNWNAXLDXRXQXBWAXMWMXQXAVTV
      SXQWTIVPXQWTFUNOIWSFUNUQNURUTVAXQXKWLBXLDXRXQXFWGXJWKXQXEWDWFXQXDWCVQXQXC
      GVPWBXQXCFUOOGWSFUOUQMURVBVCVFXQXIWIWJUIXQXHWHVQXQXGEVPWBXQXGFUPOEWSFUPUQ
      LURVBVCVGVOVHVOVHVIABHUAVJWOHWQWPDUMVKVLVMVN $.
      $( [8-Sep-2014] $)

    $( Elementhood in the set of absolute values. $)
    isabv $p |- ( R e. Ring -> ( F e. A <-> ( F : B --> ( 0 [,) +oo ) /\
    A. x e. B ( ( ( F ` x ) = 0 <-> x = Z ) /\ A. y e. B
      ( ( F ` ( x T y ) ) = ( ( F ` x ) x. ( F ` y ) ) /\
        ( F ` ( x P y ) ) <_ ( ( F ` x ) + ( F ` y ) ) ) ) ) ) ) $=
      ( vf cfv cc0 wceq co wa crg wcel cv wb cmul caddc cle wral cpnf cico cmap
      wbr wf abvfval eleq2d fveq1 eqeq1d bibi1d oveq12d eqeq12d breq12d anbi12d
      crab ralbidv elrab ovex cbs cvv fvex eqeltri elmap anbi1i bitri syl6bb )
      FUAUBZHCUBHAUCZOUCZPZQRZVPIRZUDZVPBUCZGSZVQPZVRWBVQPZUESZRZVPWBESZVQPZVRW
      EUFSZUGULZTZBDUHZTZADUHZOQUIUJSZDUKSZVCZUBZDWPHUMZVPHPZQRZVTUDZWCHPZXAWBH
      PZUESZRZWHHPZXAXEUFSZUGULZTZBDUHZTZADUHZTZVOCWRHABCDEFGOIJKLMNUNUOWSHWQUB
      ZXNTXOWOXNOHWQVQHRZWNXMADXQWAXCWMXLXQVSXBVTXQVRXAQVPVQHUPZUQURXQWLXKBDXQW
      GXGWKXJXQWDXDWFXFWCVQHUPXQVRXAWEXEUEXRWBVQHUPZUSUTXQWIXHWJXIUGWHVQHUPXQVR
      XAWEXEUFXRXSUSVAVBVDVBVDVEXPWTXNWPDHQUIUJVFDFVGPVHKFVGVIVJVKVLVMVN $.
      $( [8-Sep-2014] $)

    isabvd.1 $e |- ( ph -> R e. Ring ) $.
    isabvd.2 $e |- ( ph -> F : B --> RR ) $.
    isabvd.3 $e |- ( ph -> ( F ` Z ) = 0 ) $.
    isabvd.4 $e |- ( ( ph /\ x e. B /\ x =/= Z ) -> 0 < ( F ` x ) ) $.
    isabvd.5 $e |- ( ( ph /\
      ( x e. B /\ x =/= Z ) /\ ( y e. B /\ y =/= Z ) ) ->
      ( F ` ( x T y ) ) = ( ( F ` x ) x. ( F ` y ) ) ) $.
    isabvd.6 $e |- ( ( ph /\
      ( x e. B /\ x =/= Z ) /\ ( y e. B /\ y =/= Z ) ) ->
      ( F ` ( x P y ) ) <_ ( ( F ` x ) + ( F ` y ) ) ) $.
    $( Properties that determine an absolute value. $)
    isabvd $p |- ( ph -> F e. A ) $=
      ( wcel cc0 cpnf cico co wf cv cfv wceq wb cmul caddc cle wbr wa crg isabv
      wral syl wfn cr ffn ffvelrn sylan 0re leidi syl5breqr adantr fveq2 breq2d
      syl5ibrcom wne w3a clt 3adant3 ltle sylancr mpd 3expia pm2.61dne sylanbrc
      elrege0 ralrimiva ffnfv gt0ne0 syl2anc eqeq1d impbid 3ad2ant1 oveq1 simp3
      wi necon4d rnglz sylan9eqr fveq2d oveq1d cc recnd mul02 eqtrd oveq2 simp2
      3eqtr4d rngrz oveq2d mul01 an4 3expib syl5bi exp3a 3impib imp pm2.61da2ne
      rnggrp grplid addge02 mpbid eqbrtrd grprid addge01 jca ralrimiv mpbir2and
      cgrp ) AIDUBZEUCUDUEUFZIUGZBUHZIUIZUCUJZYJJUJZUKZYJCUHZHUFZIUIZYKYOIUIZUL
      UFZUJZYJYOFUFZIUIZYKYRUMUFZUNUOZUPZCEUSZUPZBEUSZAGUQUBZYGYIUUHUPUKPBCDEFG
      HIJKLMNOURUTAIEVAZYKYHUBZBEUSYIAEVBIUGZUUJQEVBIVCUTAUUKBEAYJEUBZUPZYKVBUB
      ZUCYKUNUOZUUKAUULUUMUUOQEVBYJIVDZVEZUUNUUPYJJUUNUUPYMUCJIUIZUNUOZAUUTUUMA
      UCUCUUSUNUCVFVGZRVHVIYMYKUUSUCUNYJJIVJZVKVLAUUMYJJVMZUUPAUUMUVCVNZUCYKVOU
      OZUUPSUVDUCVBUBUUOUVEUUPWMVFAUUMUUOUVCUURVPZUCYKVQVRVSVTWAYKWCWBWDBEYHIWE
      WBAUUGBEUUNYNUUFUUNYLYMUUNYJJYKUCAUUMUVCYKUCVMZUVDUUOUVEUVGUVFSYKWFWGVTWN
      UUNYLYMUUSUCUJZAUVHUUMRVIYMYKUUSUCUVBWHVLWIUUNUUECEAUUMYOEUBZUUEAUUMUVIVN
      ZYTUUDUVJYTYJJYOJUVJYMUPZUUSUCYQYSUVJUVHYMAUUMUVHUVIRWJZVIUVKYPJIYMUVJYPJ
      YOHUFZJYJJYOHWKUVJUUIUVIUVMJUJAUUMUUIUVIPWJZAUUMUVIWLZEGHYOJLNOWOWGWPWQUV
      KYSUCYRULUFZUCUVKYKUCYRULYMUVJYKUUSUCUVBUVLWPZWRUVKYRWSUBZUVPUCUJUVJUVRYM
      UVJYRUVJUULUVIYRVBUBZAUUMUULUVIQWJZUVOEVBYOIVDWGZWTVIYRXAUTXBXEUVJYOJUJZU
      PZUUSUCYQYSUVJUVHUWBUVLVIUWCYPJIUWBUVJYPYJJHUFZJYOJYJHXCUVJUUIUUMUWDJUJUV
      NAUUMUVIXDZEGHYJJLNOXFWGWPWQUWCYSYKUCULUFZUCUWCYRUCYKULUWBUVJYRUUSUCYOJIV
      JUVLWPZXGUWCYKWSUBZUWFUCUJUVJUWHUWBUVJYKUVJUULUUMUUOUVTUWEUUQWGZWTVIYKXHU
      TXBXEUVJUVCYOJVMZUPZYTAUUMUVIUWKYTWMAUUMUVIUPZUWKYTUWLUWKUPZUUMUVCUPZUVIU
      WJUPZUPZAYTUUMUVIUVCUWJXIZAUWNUWOYTTXJXKXLXMXNXOUVJUUDYJJYOJUVKUUBYRUUCUN
      UVKUUAYOIYMUVJUUAJYOFUFZYOYJJYOFWKUVJGYFUBZUVIUWRYOUJUVJUUIUWSUVNGXPUTZUV
      OEFGJYOLMOXQWGWPWQUVKUUPYRUUCUNUOZUVKUCUCYKUNUVAUVQVHUVJUUPUXAUKZYMUVJUVS
      UUOUXBUWAUWIYRYKXRWGVIXSXTUWCUUBYKUUCUNUWCUUAYJIUWBUVJUUAYJJFUFZYJYOJYJFX
      CUVJUWSUUMUXCYJUJUWTUWEEFGJYJLMOYAWGWPWQUWCUCYRUNUOZYKUUCUNUOZUWCUCUCYRUN
      UVAUWGVHUVJUXDUXEUKZUWBUVJUUOUVSUXFUWIUWAYKYRYBWGVIXSXTUVJUWKUUDAUUMUVIUW
      KUUDWMAUWLUWKUUDUWMUWPAUUDUWQAUWNUWOUUDUAXJXKXLXMXNXOYCVTYDYCWDYE $.
      $( [8-Sep-2014] $)
  $}

  ${
    $d x y B $.  $d x y F $.  $d x y P $.  $d x y R $.  $d x y T $.  $d y Y $.
    $d f r x y $.  $d x y X $.  $d x Z $.
    abvf.a $e |- A = ( AbsVal ` R ) $.
    $( Reverse closure for the absolute value set. $)
    abvrcl $p |- ( F e. A -> R e. Ring ) $=
      ( vx vf vr vy crg wcel cabv cfv cdm cv cc0 wceq co wa wral crab c0g cmulr
      wb cmul cplusg caddc cle wbr cbs cpnf cico cmap cvv df-abv ssrab2 eqsstri
      dmmpt elfvdm sseldi eleq2s ) BIJCBKLZACVAJKMZIBVBENZFNZLZOPVCGNZUALPUCVCH
      NZVFUBLQVDLVEVGVDLZUDQPVCVGVFUELQVDLVEVHUFQUGUHRHVFUILZSREVISFOUJUKQVIULQ
      TZUMJZGITIGIVJKEHFGUNUQVKGIUOUPCBKURUSDUT $.
      $( [8-Sep-2014] $)

    abvf.b $e |- B = ( Base ` R ) $.
    $( An absolute value is a function from the ring to the nonnegative real
       numbers. $)
    abvfge0 $p |- ( F e. A -> F : B --> ( 0 [,) +oo ) ) $=
      ( vx vy wcel cc0 cpnf cico co cv cfv wceq wb wa wral eqid wf cmulr cplusg
      c0g cmul caddc cle wbr crg abvrcl isabv syl ibi simpld ) DAIZBJKLMDUAZGNZ
      DOZJPUQCUDOZPQUQHNZCUBOZMDOURUTDOZUEMPUQUTCUCOZMDOURVBUFMUGUHRHBSRGBSZUOU
      PVDRZUOCUIIUOVEQACDEUJGHABVCCVADUSEFVCTVATUSTUKULUMUN $.
      $( [8-Sep-2014] $)

    $( An absolute value is a function from the ring to the real numbers. $)
    abvf $p |- ( F e. A -> F : B --> RR ) $=
      ( wcel cc0 cpnf cico co wf cr wss abvfge0 cxr 0re pnfxr icossre mp2an fss
      sylancl ) DAGBHIJKZDLUCMNZBMDLABCDEFOHMGIPGUDQRHISTBUCMDUAUB $.
      $( [8-Sep-2014] $)

    $( An absolute value is a function from the ring to the real numbers. $)
    abvcl $p |- ( ( F e. A /\ X e. B ) -> ( F ` X ) e. RR ) $=
      ( wcel cr wf cfv abvf ffvelrn sylan ) DAHBIDJEBHEDKIHABCDFGLBIEDMN $.
      $( [8-Sep-2014] $)

    $( The absolute value of a number is greater or equal to zero. $)
    abvge0 $p |- ( ( F e. A /\ X e. B ) -> 0 <_ ( F ` X ) ) $=
      ( wcel wa cfv cc0 cpnf cico co cle wbr wf abvfge0 ffvelrn sylan elrege0
      cr simprbi syl ) DAHZEBHZIEDJZKLMNZHZKUGOPZUEBUHDQUFUIABCDFGRBUHEDSTUIUGU
      BHUJUGUAUCUD $.
      $( [8-Sep-2014] $)

    ${
      abveq0.z $e |- Z = ( 0g ` R ) $.
      $( The value of an absolute value is zero iff the argument is zero. $)
      abveq0 $p |- ( ( F e. A /\ X e. B ) -> ( ( F ` X ) = 0 <-> X = Z ) ) $=
        ( vx vy wcel cv cfv cc0 wceq wb wral co wa cmulr cmul cplusg caddc cpnf
        cle wbr cico wf crg abvrcl eqid isabv syl ibi simprd simpl ralimi fveq2
        eqeq1d eqeq1 bibi12d rcla4cva sylan ) DALZJMZDNZOPZVFFPZQZJBRZEBLEDNZOP
        ZEFPZQZVEVJVFKMZCUANZSDNVGVPDNZUBSPVFVPCUCNZSDNVGVRUDSUFUGTKBRZTZJBRZVK
        VEBOUEUHSDUIZWBVEWCWBTZVECUJLVEWDQACDGUKJKABVSCVQDFGHVSULVQULIUMUNUOUPW
        AVJJBVJVTUQURUNVJVOJEBVFEPZVHVMVIVNWEVGVLOVFEDUSUTVFEFVAVBVCVD $.
        $( [8-Sep-2014] $)

      $( The absolute value of a nonzero number is nonzero. $)
      abvne0 $p |- ( ( F e. A /\ X e. B /\ X =/= Z ) -> ( F ` X ) =/= 0 ) $=
        ( wcel cfv cc0 wne wa abveq0 necon3bid biimp3ar ) DAJZEBJZEDKZLMEFMRSNT
        LEFABCDEFGHIOPQ $.
        $( [8-Sep-2014] $)

      $( The absolute value of a nonzero number is strictly positive. $)
      abvgt0 $p |- ( ( F e. A /\ X e. B /\ X =/= Z ) -> 0 < ( F ` X ) ) $=
        ( wcel wne w3a cc0 cfv clt wbr cle cr wa 3adant3 wb abvcl ltlen sylancr
        0re abvge0 abvne0 mpbir2and ) DAJZEBJZEFKZLZMEDNZOPZMUMQPZUMMKZULMRJUMR
        JZUNUOUPSUAUEUIUJUQUKABCDEGHUBTMUMUCUDUIUJUOUKABCDEGHUFTABCDEFGHIUGUH
        $.
        $( [8-Sep-2014] $)
    $}

    ${
      abvmul.t $e |- T = ( .r ` R ) $.
      $( An absolute value distributes under multiplication. $)
      abvmul $p |- ( ( F e. A /\ X e. B /\ Y e. B ) ->
        ( F ` ( X T Y ) ) = ( ( F ` X ) x. ( F ` Y ) ) ) $=
        ( vx vy wcel co cfv cmul wceq cv wral wa cc0 c0g wb cplusg cle wbr cpnf
        caddc cico wf crg abvrcl isabv syl ibi simprd simpl ralimi adantl oveq1
        eqid fveq2d fveq2 oveq1d eqeq12d oveq2 oveq2d rcla42v syl5com 3impib )
        EAMZFBMZGBMZFGDNZEOZFEOZGEOZPNZQZVKKRZLRZDNZEOZVTEOZWAEOZPNZQZLBSZKBSZV
        LVMTVSVKWDUAQVTCUBOZQUCZWGVTWACUDOZNEOWDWEUHNUEUFZTZLBSZTZKBSZWIVKBUAUG
        UINEUJZWQVKWRWQTZVKCUKMVKWSUCACEHULKLABWLCDEWJHIWLVAJWJVAUMUNUOUPWPWHKB
        WOWHWKWNWGLBWGWMUQURUSURUNWGVSFWADNZEOZVPWEPNZQKLFGBBVTFQZWCXAWFXBXCWBW
        TEVTFWADUTVBXCWDVPWEPVTFEVCVDVEWAGQZXAVOXBVRXDWTVNEWAGFDVFVBXDWEVQVPPWA
        GEVCVGVEVHVIVJ $.
        $( [8-Sep-2014] $)
    $}

    abvtri.p $e |- P = ( +g ` R ) $.
    $( An absolute value satisfies the triangle inequality. $)
    abvtri $p |- ( ( F e. A /\ X e. B /\ Y e. B ) ->
      ( F ` ( X P Y ) ) <_ ( ( F ` X ) + ( F ` Y ) ) ) $=
      ( vx vy wcel co cfv caddc cle wral wa wceq wbr cv cc0 c0g cmulr cmul cpnf
      wb cico wf crg abvrcl eqid isabv syl ibi simprd simpr ralimi adantl oveq1
      fveq2d fveq2 oveq1d breq12d oveq2 oveq2d rcla42v syl5com 3impib ) EAMZFBM
      ZGBMZFGCNZEOZFEOZGEOZPNZQUAZVKKUBZLUBZCNZEOZVTEOZWAEOZPNZQUAZLBRZKBRZVLVM
      SVSVKWDUCTVTDUDOZTUHZVTWADUEOZNEOWDWEUFNTZWGSZLBRZSZKBRZWIVKBUCUGUINEUJZW
      QVKWRWQSZVKDUKMVKWSUHADEHULKLABCDWLEWJHIJWLUMWJUMUNUOUPUQWPWHKBWOWHWKWNWG
      LBWMWGURUSUTUSUOWGVSFWACNZEOZVPWEPNZQUAKLFGBBVTFTZWCXAWFXBQXCWBWTEVTFWACV
      AVBXCWDVPWEPVTFEVCVDVEWAGTZXAVOXBVRQXDWTVNEWAGFCVFVBXDWEVQVPPWAGEVCVGVEVH
      VIVJ $.
      $( [8-Sep-2014] $)
  $}

  ${
    abv0.a $e |- A = ( AbsVal ` R ) $.
    ${
      abv0.z $e |- Z = ( 0g ` R ) $.
      $( The absolute value of zero is zero. $)
      abv0 $p |- ( F e. A -> ( F ` Z ) = 0 ) $=
        ( wcel cbs cfv cc0 wceq crg abvrcl eqid rng0cl syl abveq0 mpbiri mpdan
        wa ) CAGZDBHIZGZDCIJKZUABLGUCABCEMUBBDUBNZFOPUAUCTUDDDKDNAUBBCDDEUEFQRS
        $.
        $( [8-Sep-2014] $)
    $}

    ${
      abv1.p $e |- U = ( 1r ` R ) $.
      ${
        abv1z.z $e |- Z = ( 0g ` R ) $.
        $( The absolute value of one is one in a non-trivial ring. $)
        abv1z $p |- ( ( F e. A /\ U =/= Z ) -> ( F ` U ) = 1 ) $=
          ( wcel wne wa cfv cdiv co c1 wceq adantr eqid syl2anc syl3anc crg cbs
          cmulr abvrcl rngidcl syl rnglidm fveq2d simpl abvmul eqtr3d oveq1d cc
          cmul cc0 cr abvcl mpdan recnd simpr abvne0 divid divcan3 3eqtr3rd ) D
          AIZCEJZKZCDLZVHMNZVHVHUNNZVHMNZOVHVGVHVJVHMVGCCBUCLZNZDLZVHVJVGVMCDVG
          BUAIZCBUBLZIZVMCPVEVOVFABDFUDZQVEVQVFVEVOVQVRVPBCVPRZGUEUFZQZVPBVLCCV
          SVLRZGUGSUHVGVEVQVQVNVJPVEVFUIZWAWAAVPBVLDCCFVSWBUJTUKULVGVHUMIZVHUOJ
          ZVIOPVGVHVEVHUPIZVFVEVQWFVTAVPBDCFVSUQURQUSZVGVEVQVFWEWCWAVEVFUTAVPBD
          CEFVSHVATZVHVBSVGWDWDWEVKVHPWGWGWHVHVHVCTVD $.
          $( [8-Sep-2014] $)
      $}

      $( The absolute value of one is one in a division ring. $)
      abv1 $p |- ( ( R e. DivRing /\ F e. A ) -> ( F ` U ) = 1 ) $=
        ( wcel c0g cfv wne c1 wceq cdr id eqid drngunz abv1z syl2anr ) DAGZSCBH
        IZJCDIKLBMGSNBCTTOZFPABCDTEFUAQR $.
        $( [8-Sep-2014] $)
    $}

    abvneg.b $e |- B = ( Base ` R ) $.
    ${
      abvneg.p $e |- N = ( invg ` R ) $.
      $( The absolute value of a negative is the same as that of the
         positive. $)
      abvneg $p |- ( ( F e. A /\ X e. B ) ->
        ( F ` ( N ` X ) ) = ( F ` X ) ) $=
        ( wcel wa cfv wceq adantr syl eqid fveq2d cmul co c1 cur c0g crg abvrcl
        wi cgrp rnggrp grpinvcl sylan simpr rng1eq0 syl3anc imp wne c2 cmulr cc
        cexp cr rngidcl syl2anc abvcl mpdan recnd sqval abvmul mpd3an23 rngnegl
        rngnegrmul grpinvinv 3eqtr3d 3eqtr2d abv1z eqtrd sq1 syl6eqr cc0 cle wb
        wbr abvge0 1re 0re lt01 ltleii sq11 mpanr12 biimpa syldan adantlr simpl
        oveq1d eqtr3d mulid2 pm2.61dane ) DAJZFBJZKZFELZDLZFDLZMCUALZCUBLZWRXBX
        CMZKWSFDWRXDWSFMZWRCUCJZWSBJZWQXDXEUEWPXFWQACDGUDZNZWPCUFJZWQXGWPXFXJXH
        CUGOZBCEFHIUHUIWPWQUJZBCXBWSFXCHXBPZXCPZUKULUMQWRXBXCUNZKZXBELZDLZXARSZ
        TXARSZWTXAXPXRTXARWPXOXRTMZWQWPXOXRUOURSZTUOURSZMZYAWPXOKZYBTYCYEYBXBDL
        ZTWPYBYFMXOWPYBXRXRRSZXQXQCUPLZSZDLZYFWPXRUQJYBYGMWPXRWPXQBJZXRUSJZWPXJ
        XBBJZYKXKWPXFYMXHBCXBHXMUTOZBCEXBHIUHVAZABCDXQGHVBVCZVDXRVEOWPYKYKYJYGM
        YOYOABCYHDXQXQGHYHPZVFVGWPYIXBDWPXQXBYHSZELXQELZYIXBWPYRXQEWPBCYHXBEXBH
        YQXMIXHYNVHQWPBCYHEXQXBHYQIXHYOYNVIWPXJYMYSXBMXKYNBCEXBHIVJVAVKQVLNACXB
        DXCGXMXNVMVNVOVPWPYDYAWPYLVQXRVRVTZYDYAVSZYPWPYKYTYOABCDXQGHWAVCYLYTKTU
        SJVQTVRVTUUAWBVQTWCWBWDWEXRTWFWGVAWHWIWJWLWRXSWTMXOWRXQFYHSZDLZXSWTWRWP
        YKWQUUCXSMWPWQWKWPYKWQYONXLABCYHDXQFGHYQVFULWRUUBWSDWRBCYHXBEFHYQXMIXIX
        LVHQWMNWRXTXAMZXOWRXAUQJUUDWRXAABCDFGHVBVDXAWNONVKWO $.
        $( [8-Sep-2014] $)
    $}

    abvrec.z $e |- Z = ( 0g ` R ) $.
    ${
      abvrec.p $e |- I = ( invr ` R ) $.
      $( The absolute value distributes under reciprocal.  (Contributed by
         Mario Carneiro, 10-Sep-2014.) $)
      abvrec $p |- ( ( ( R e. DivRing /\ F e. A ) /\ ( X e. B /\ X =/= Z ) ) ->
                     ( F ` ( I ` X ) ) = ( 1 / ( F ` X ) ) ) $=
        ( wcel wa wne cfv co cdiv c1 wceq syl3anc cdr cmul cc cc0 simplr simpll
        cr simprl simprr drnginvrcl abvcl syl2anc recnd abvne0 divcan3 cur eqid
        cmulr drnginvrr fveq2d abvmul abv1 adantr 3eqtr3d oveq1d eqtr3d ) CUALZ
        DALZMZFBLZFGNZMZMZFDOZFEOZDOZUBPZVNQPZVPRVNQPVMVPUCLVNUCLVNUDNZVRVPSVMV
        PVMVHVOBLZVPUGLVGVHVLUEZVMVGVJVKVTVGVHVLUFZVIVJVKUHZVIVJVKUIZBCEFGIJKUJ
        TZABCDVOHIUKULUMVMVNVMVHVJVNUGLWAWCABCDFHIUKULUMVMVHVJVKVSWAWCWDABCDFGH
        IJUNTVPVNUOTVMVQRVNQVMFVOCUROZPZDOZCUPOZDOZVQRVMWGWIDVMVGVJVKWGWISWBWCW
        DBCWFWIEFGIJWFUQZWIUQZKUSTUTVMVHVJVTWHVQSWAWCWEABCWFDFVOHIWKVATVIWJRSVL
        ACWIDHWLVBVCVDVEVF $.
        $( [10-Sep-2014] $)
    $}

    ${
      abvdiv.p $e |- D = ( /r ` R ) $.
      $( The absolute value distributes under division.  (Contributed by Mario
         Carneiro, 10-Sep-2014.) $)
      abvdiv $p |- ( ( ( R e. DivRing /\ F e. A ) /\
                       ( X e. B /\ Y e. B /\ Y =/= Z ) ) ->
                     ( F ` ( X D Y ) ) = ( ( F ` X ) / ( F ` Y ) ) ) $=
        ( wcel wa cfv co cmul wceq eqid syl3anc cdr wne w3a cinvr cmulr c1 cdiv
        simplr simpr1 simpll simpr2 simpr3 drnginvrcl abvmul 3adantr1 eqtrd cui
        abvrec oveq2d wb drngunit syl mpbir2and rngdvval fveq2d cc cc0 cr abvcl
        syl2anc recnd abvne0 divrec 3eqtr4d ) DUAMZEAMZNZFBMZGBMZGHUBZUCZNZFGDU
        DOZOZDUEOZPZEOZFEOZUFGEOZUGPZQPZFGCPZEOWHWIUGPZWBWGWHWDEOZQPZWKWBVPVRWD
        BMZWGWORVOVPWAUHZVQVRVSVTUIZWBVOVSVTWPVOVPWAUJZVQVRVSVTUKZVQVRVSVTULZBD
        WCGHJKWCSZUMTABDWEEFWDIJWESZUNTWBWNWJWHQVQVSVTWNWJRVRABDEWCGHIJKXBURUOU
        SUPWBWLWFEWBBCDWEDUQOZWCUAFGJXCXDSZXBLWSWRWBGXDMZVSVTWBVOXFVSVTNUTWSBDX
        DGHJXEKVAVBWTXAVCVDVEWBWHVFMWIVFMWIVGUBZWMWKRWBWHWBVPVRWHVHMWQWRABDEFIJ
        VIVJVKWBWIWBVPVSWIVHMWQWTABDEGIJVIVJVKWBVPVSVTXGWQWTXAABDEGHIJKVLTWHWIV
        MTVN $.
        $( [10-Sep-2014] $)
    $}

    ${
      abvdom.t $e |- T = ( .r ` R ) $.
      $( Any ring with an absolute value is a domain, which is to say that it
         contains no zero divisors.  (Contributed by Mario Carneiro,
         10-Sep-2014.) $)
      abvdom $p |- ( ( F e. A /\ ( X e. B /\ X =/= Z ) /\
        ( Y e. B /\ Y =/= Z ) ) -> ( X T Y ) =/= Z ) $=
        ( wcel wne wa co cfv cc0 wceq syl3anc w3a simp1 simp2l simp3l abvmul cc
        cmul cr abvcl syl2anc simp2r abvne0 simp3r mulne0 syl22anc eqnetrd abv0
        recnd syl fveq2 eqeq1d syl5ibrcom necon3d mpd ) EAMZFBMZFHNZOZGBMZGHNZO
        ZUAZFGDPZEQZRNVMHNVLVNFEQZGEQZUGPZRVLVEVFVIVNVQSVEVHVKUBZVEVFVGVKUCZVEV
        HVIVJUDZABCDEFGIJLUETVLVOUFMVORNZVPUFMVPRNZVQRNVLVOVLVEVFVOUHMVRVSABCEF
        IJUIUJURVLVEVFVGWAVRVSVEVFVGVKUKABCEFHIJKULTVLVPVLVEVIVPUHMVRVTABCEGIJU
        IUJURVLVEVIVJWBVRVTVEVHVIVJUMABCEGHIJKULTVOVPUNUOUPVLVMHVNRVLVNRSVMHSZH
        EQZRSZVLVEWEVRACEHIKUQUSWCVNWDRVMHEUTVAVBVCVD $.
        $( [10-Sep-2014] $)
    $}
  $}

  ${
    $d x y z B $.  $d y z F $.  $d x y z R $.  $d x Z $.
    abvtriv.a $e |- A = ( AbsVal ` R ) $.
    abvtriv.b $e |- B = ( Base ` R ) $.
    abvtriv.z $e |- Z = ( 0g ` R ) $.
    abvtriv.f $e |- F = ( x e. B |-> if ( x = Z , 0 , 1 ) ) $.
    $( The trivial absolute value.  (This theorem is true as long as ` R ` is
       an integral domain, but it is not true for rings with zero divisors,
       which violate the multiplication axiom; ~ abvdom is the contrapositive
       of this remark.)  (Contributed by Mario Carneiro, 8-Sep-2014.) $)
    abvtriv $p |- ( R e. DivRing -> F e. A ) $=
      ( wcel cfv wceq cc0 c1 cif co syl cle c2 vy cdr cplusg cmulr eqid drngrng
      vz cv cr wa 0re 1re keepel a1i fmptd crg rng0cl iftrue elexi 3syl wne w3a
      fvmpt clt lt01 eqeq1 ifbid ifex wn df-ne iffalse sylbi sylan9eq syl5breqr
      3adant1 cmul ax-1cn mulid1i eqcomi csn cdif biid eldifsn drngmcl syl3anbr
      sylib simpld simprd eqtrd simp2l simp2r simp3l simp3r oveq12d 3eqtr4a wbr
      caddc breq1 2re 2pos ltleii 1lt2 keephyp df-2 breqtri cgrp 3ad2ant1 grpcl
      drnggrp syl3anc 3brtr4d isabvd ) DUBKZUAUGBCDUCLZDDUDLZEFGHXNUEZXOUEZIDUF
      ZXMACAUHZFMZNOPZUIEYAUIKXMXSCKUJXTNOUIUKULUMUNJUOXMDUPKFCKFELNMXRCDFHIUQA
      FYANCEXTNOURJNUIUKUSZVCUTXMUAUHZCKZYCFVAZVBNOYCELZVDVEYDYEYFOMXMYDYEYFYCF
      MZNOPZOAYCYAYHCEXSYCMXTYGNOXSYCFVFVGJYGNOYBOUIULUSZVHVCZYEYGVIZYHOMZYCFVJ
      ZYGNOVKZVLVMVOVNXMYDYEUJZUGUHZCKZYPFVAZUJZVBZOOOVPQZYCYPXOQZELZYFYPELZVPQ
      UUAOOVQVRVSYTUUCUUBFMZNOPZOYTUUBCKZUUCUUFMYTUUGUUBFVAZYTUUBCFVTWAZKZUUGUU
      HUJXMXMYOYCUUIKYSYPUUIKUUJXMWBYCCFWCYPCFWCCDXOYCYPFHXQIWDWEUUBCFWCWFZWGAU
      UBYAUUFCEXSUUBMXTUUENOXSUUBFVFVGJUUENOYBYIVHVCRYTUUEVIZUUFOMYTUUHUULYTUUG
      UUHUUKWHUUBFVJWFUUENOVKRWIYTYFOUUDOVPYTYFYHOYTYDYFYHMXMYDYEYSWJZYJRYTYKYL
      YTYEYKXMYDYEYSWKYMWFYNRWIZYTUUDYPFMZNOPZOYTYQUUDUUPMXMYOYQYRWLZAYPYAUUPCE
      XSYPMXTUUONOXSYPFVFVGJUUONOYBYIVHVCRYTUUOVIZUUPOMYTYRUURXMYOYQYRWMYPFVJWF
      UUONOVKRWIZWNWOYTYCYPXNQZFMZNOPZOOWQQZUUTELZYFUUDWQQSUVBUVCSWPYTUVBTUVCSU
      VANTSWPOTSWPUVBTSWPNONUVBTSWROUVBTSWRNTUKWSWTXAOTULWSXBXAXCXDXEUNYTUUTCKZ
      UVDUVBMYTDXFKZYDYQUVEXMYOUVFYSDXIXGUUMUUQCXNDYCYPHXPXHXJAUUTYAUVBCEXSUUTM
      XTUVANOXSUUTFVFVGJUVANOYBYIVHVCRYTYFOUUDOWQUUNUUSWNXKXL $.
      $( [8-Sep-2014] $)
  $}


