$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Monoids
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Mnd $.
  $c Grp $.
  $c 0g $.
  $c invg $.
  $c -g $.
  $c .g $.

  $( Extend class notation with class of all monoids. $)
  cmnd $a class Mnd $.

  $( Extend class notation with class of all groups. $)
  cgrp $a class Grp $.

  $( Extend class notation with group identity element. $)
  c0g $a class 0g $.

  $( Extend class notation with inverse of group element. $)
  cminusg $a class invg $.

  $( Extend class notation with group subtraction (or division) operation. $)
  csg $a class -g $.

  $( Extend class notation with a function mapping a group operation to the
     power operation for the group. $)
  cmg $a class .g $.

  ${
    $d b e g p x y z $.
    $( Definition of a monoid.  A monoid is a set equipped with an everywhere
       defined internal operation (so, a magma, see ~ mndcl ), whose operation
       is associative (so, a semigroup, see ~ mndass ) and has a two-sided
       neutral element (see ~ mndid ). $)
    df-mnd $a |- Mnd = { g | [ ( Base ` g ) / b ] [ ( +g ` g ) / p ]
       ( A. x e. b A. y e. b A. z e. b ( ( x p y ) e. b
            /\ ( ( x p y ) p z ) = ( x p ( y p z ) ) )
            /\ E. e e. b A. x e. b ( ( e p x ) = x /\ ( x p e ) = x ) ) } $.

    $( Define group identity element. $)
    df-0g $a |- 0g = ( g e. _V |-> ( iota_ e e. ( Base ` g )
      A. x e. ( Base ` g )
        ( ( e ( +g ` g ) x ) = x /\ ( x ( +g ` g ) e ) = x ) ) ) $.
  $}

  ${
    $d a b c e g p v B $.  $d a b c e g p v G $.  $d a b c e g p v P $.
    ismnd.b $e |- B = ( Base ` G ) $.
    ismnd.p $e |- P = ( +g ` G ) $.
    $( The predicate "is a monoid."  (Contributed by Mario Carneiro,
       6-Jan-2015.) $)
    ismnd $p |- ( G e. Mnd <-> ( A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ ( a P e ) = a ) ) ) $=
      ( vp vv vg wcel cv co wceq wa wral cbs cvv cmnd wrex cplusg df-mnd eleq2i
      cfv wsbc cab c0 wne rexn0 fvprc syl5eq necon1ai syl adantl fvex a1i fveq2
      wn syl6eqr simpl fveq2d simplr simpr oveqd eleq12d eqidd oveq123d eqeq12d
      anbi12d raleqbidv eqeq1d rexeqbidv sbcied2 elab3 bitri ) DUAMDENZFNZJNZOZ
      KNZMZWAGNZVTOZVRVSWDVTOZVTOZPZQZGWBRZFWBRZEWBRZCNZVRVTOZVRPZVRWMVTOZVRPZQ
      ZEWBRZCWBUBZQZJLNZUCUFZUGZKXBSUFZUGZLUHZMVRVSBOZAMZXHWDBOZVRVSWDBOZBOZPZQ
      ZGARZFARZEARZWMVRBOZVRPZVRWMBOZVRPZQZEARZCAUBZQZUAXGDEFGCLJKUDUEXFYELDYDD
      TMZXQYDAUIUJYFYCCAUKYFAUIYFUTADSUFZUIHDSULUMUNUOUPXBDPZXDYEKXEATXETMYHXBS
      UQURYHXEYGAXBDSUSHVAYHWBAPZQZXAYEJXCBTXCTMYJXBUCUQURYJXCDUCUFBYJXBDUCYHYI
      VBVCIVAYJVTBPZQZWLXQWTYDYLWKXPEWBAYHYIYKVDZYLWJXOFWBAYMYLWIXNGWBAYMYLWCXI
      WHXMYLWAXHWBAYLVTBVRVSYJYKVEZVFZYMVGYLWEXJWGXLYLWAXHWDWDVTBYNYOYLWDVHVIYL
      VRVRWFXKVTBYNYLVRVHYLVTBVSWDYNVFVIVJVKVLVLVLYLWSYCCWBAYMYLWRYBEWBAYMYLWOX
      SWQYAYLWNXRVRYLVTBWMVRYNVFVMYLWPXTVRYLVTBVRWMYNVFVMVKVLVNVKVOVOVPVQ $.
      $( [6-Jan-2015] $)
  $}

  ${
    $d u w x B $.  $d u w x P $.
    $( A two-sided identity element is unique (if it exists) in any magma.
       (Contributed by Mario Carneiro, 7-Dec-2014.) $)
    mgmidmo $p |- E* u ( u e. B /\
      A. x e. B ( ( u P x ) = x /\ ( x P u ) = x ) ) $=
      ( vw cv wcel co wceq wa wral ralimi oveq1 id eqeq12d rcla4va oveq2 eqeq1d
      wal an4s wmo simpl simpr anim12i eqtr2 syl2an ancom2s sylan2 gen2 anbi12d
      wi eleq1 ralbidv mo4 mpbir ) BFZCGZUPAFZDHZURIZURUPDHZURIZJZACKZJZBUAVEEF
      ZCGZVFURDHZURIZURVFDHZURIZJZACKZJZJUPVFIZUKZESBSVPBEUQVGVDVMVOVDVMJUQVGJZ
      UTACKZVKACKZJVOVDVRVMVSVCUTACUTVBUBLVLVKACVIVKUCLUDVQVSVRVOUQVSVGVRVOUQVS
      JUPVFDHZUPIZVTVFIZVOVGVRJVKWAAUPCURUPIZVJVTURUPURUPVFDMWCNOPUTWBAVFCURVFI
      ZUSVTURVFURVFUPDQWDNOPVTUPVFUEUFTUGUHTUIVEVNBEVOUQVGVDVMUPVFCULVOVCVLACVO
      UTVIVBVKVOUSVHURUPVFURDMRVOVAVJURUPVFURDQRUJUMUJUNUO $.
      $( [7-Dec-2014] $)
  $}

  ${
    $d u w x y z B $.  $d u w x y z G $.  $d x y z X $.  $d y z Y $.  $d z Z $.
    $d u w x y z P $.
    mndlem1.b $e |- B = ( Base ` G ) $.
    mndlem1.p $e |- P = ( +g ` G ) $.
    $( Lemma for monoid properties. $)
    mndlem1 $p |- ( ( G e. Mnd /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
       ( ( X P Y ) e. B /\ ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) ) $=
      ( vx vy vz vw wcel co wceq wa cv wral oveq1 eqeq12d w3a cmnd wrex simplbi
      ismnd eleq1d oveq1d anbi12d oveq2 oveq2d anbi2d rcla43v syl5 impcom ) DAM
      EAMFAMUAZCUBMZDEBNZAMZUQFBNZDEFBNZBNZOZPZUPIQZJQZBNZAMZVFKQZBNZVDVEVHBNZB
      NZOZPZKARJARIARZUOVCUPVNLQZVDBNVDOVDVOBNVDOPIARLAUCABLCIJKGHUEUDVMVCDVEBN
      ZAMZVPVHBNZDVJBNZOZPURUQVHBNZDEVHBNZBNZOZPIJKDEFAAAVDDOZVGVQVLVTWEVFVPAVD
      DVEBSZUFWEVIVRVKVSWEVFVPVHBWFUGVDDVJBSTUHVEEOZVQURVTWDWGVPUQAVEEDBUIZUFWG
      VRWAVSWCWGVPUQVHBWHUGWGVJWBDBVEEVHBSUJTUHVHFOZWDVBURWIWAUSWCVAVHFUQBUIWIW
      BUTDBVHFEBUIUJTUKULUMUN $.
      $( [6-Jan-2015] $) $( [14-Aug-2011] $)

    $( Closure of the operation of a monoid. $)
    mndcl $p |- ( ( G e. Mnd /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( cmnd wcel co w3a df-3an anabs1 bitri wceq mndlem1 simpld sylan2br 3impb
      wa ) CHIZDAIZEAIZDEBJZAIZUBUCTZUAUBUCUBKZUEUGUFUBTUFUBUCUBLUBUCMNUAUGTUEU
      DDBJDEDBJBJOABCDEDFGPQRS $.
      $( [6-Jan-2015] $) $( [14-Aug-2011] $)

    $( A monoid operation is associative. $)
    mndass $p |- ( ( G e. Mnd /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
              ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) $=
      ( cmnd wcel w3a wa co wceq mndlem1 simprd ) CIJDAJEAJFAJKLDEBMZAJQFBMDEFB
      MBMNABCDEFGHOP $.
      $( [14-Aug-2011] $)

    $( A monoid has a two-sided identity element. $)
    mndid $p |- ( G e. Mnd ->
         E. u e. B A. x e. B ( ( u P x ) = x /\ ( x P u ) = x ) ) $=
      ( vy vz cmnd wcel cv co wceq wa wral wrex ismnd simprbi ) EJKALZHLZDMZCKU
      BILZDMTUAUCDMDMNOICPHCPACPBLZTDMTNTUDDMTNOACPBCQCDBEAHIFGRS $.
      $( [16-Aug-2011] $)

    $( The two-sided identity element of a monoid is unique.  Lemma 2.2.1(a) of
       [Herstein] p. 55.  (Contributed by Mario Carneiro, 8-Dec-2014.) $)
    mndideu $p |- ( G e. Mnd ->
      E! u e. B A. x e. B ( ( u P x ) = x /\ ( x P u ) = x ) ) $=
      ( cmnd wcel cv co wceq wa wral wrex wmo wreu mndid mgmidmo a1i sylanbrc
      reu5 ) EHIZBJZAJZDKUELUEUDDKUELMACNZBCOUDCIUFMBPZUFBCQABCDEFGRUGUCABCDSTU
      FBCUBUA $.
      $( [8-Dec-2014] $)
  $}

  ${
    $d a b c $.
    $( The group zero extractor is a function.  (Contributed by Stefan O'Rear,
       10-Jan-2015.) $)
    fn0g $p |- 0g Fn _V $=
      ( vb vc va cv cplusg cfv co wceq wa cbs wral crio cvv c0g wfn df-0g fnmpt
      wcel riotaex a1i mprg ) ADZBDZCDZEFZGUCHUCUBUEGUCHIBUDJFZKZAUFLZMRZNMOCMC
      MUHNMBACPQUIUDMRUGAUFSTUA $.
      $( [10-Jan-2015] $)
  $}

  ${
    $d e g x B $.  $d e g x G $.  $d g P $.
    grpidval.b $e |- B = ( Base ` G ) $.
    grpidval.p $e |- P = ( +g ` G ) $.
    grpidval.o $e |- O = ( 0g ` G ) $.
    $( The value of the identity element of a group. $)
    grpidval $p |- ( G e. V -> O =
      ( iota_ e e. B A. x e. B ( ( e P x ) = x /\ ( x P e ) = x ) ) ) $=
      ( vg wcel cvv cv co wceq wa cfv cplusg cbs wral crio elex c0g fveq2 oveqd
      syl6eqr eqeq1d anbi12d raleqbidv riotaeqbidv df-0g riotaex fvmpt syl5eq
      syl ) EGLEMLZFDNZANZCOZUSPZUSURCOZUSPZQZABUAZDBUBZPEGUCUQFEUDRVFJKEURUSKN
      ZSRZOZUSPZUSURVHOZUSPZQZAVGTRZUAZDVNUBVFMUDVGEPZVOVEDVNBVPVNETRBVGETUEHUG
      ZVPVMVDAVNBVQVPVJVAVLVCVPVIUTUSVPVHCURUSVPVHESRCVGESUEIUGZUFUHVPVKVBUSVPV
      HCUSURVRUFUHUIUJUKADKULVEDBUMUNUOUP $.
      $( [7-Aug-2013] $) $( [20-Aug-2011] $)
  $}

  ${
    $d e x y B $.  $d e x y G $.  $d e x y O $.  $d e x y P $.  $d e x y U $.
    $d x X $.
    mndidcl.b $e |- B = ( Base ` G ) $.
    mndidcl.o $e |- O = ( 0g ` G ) $.
    ${
      mgmidcl.p $e |- P = ( +g ` G ) $.
      mgmidcl.e $e |- ( ph ->
        E. e e. B A. x e. B ( ( e P x ) = x /\ ( x P e ) = x ) ) $.
      $( The identity element of a magma, if it exists, belongs to the base
         set. $)
      ismgmid $p |- ( ph -> ( ( U e. B /\
        A. x e. B ( ( U P x ) = x /\ ( x P U ) = x ) ) <-> O = U ) ) $=
        ( wcel cv co wceq wa eqeq1d syl c0 vy wral crio wreu wb id wrex mgmidmo
        wmo a1i reu5 sylanbrc ax-17 oveq1 oveq2 anbi12d ralbidv riota2f syl2anr
        a17d pm5.32da riotacl eleq1 syl5ibcom pm4.71rd bitr4d cvv wne rexn0 cbs
        wn cfv fvprc syl5eq necon1ai grpidval ) AECMZEBNZDOZVRPZVREDOZVRPZQZBCU
        BZQZFNZVRDOZVRPZVRWFDOZVRPZQZBCUBZFCUCZEPZHEPAWEVQWNQWNAVQWDWNVQVQWLFCU
        DZWDWNUEAVQUFAWLFCUGZWFCMWLQFUIZWOLWQABFCDUHUJWLFCUKULZWLWDFUACEUANEMFU
        MVQWDFUTWFEPZWKWCBCWSWHVTWJWBWSWGVSVRWFEVRDUNRWSWIWAVRWFEVRDUORUPUQURUS
        VAAWNVQAWMCMZWNVQAWOWTWRWLFCVBSWMECVCVDVEVFAHWMEAGVGMZHWMPACTVHZXAAWPXB
        LWLFCVISXACTXAVKCGVJVLTIGVJVMVNVOSBCDFGHVGIKJVPSRVF $.
        $( [27-Dec-2014] $)

      $( The identity element of a magma, if it exists, belongs to the base
         set. $)
      mgmidcl $p |- ( ph -> O e. B ) $=
        ( wcel cv co wceq wa wral eqid ismgmid mpbiri simpld ) AGCLZGBMZDNUCOUC
        GDNUCOPBCQZAUBUDPGGOGRABCDGEFGHIJKSTUA $.
        $( [27-Dec-2014] $)

      $( The identity element of a magma, if it exists, is a left and right
         identity. $)
      mgmlrid $p |- ( ( ph /\ X e. B ) ->
        ( ( O P X ) = X /\ ( X P O ) = X ) ) $=
        ( cv co wceq wa wral wcel eqid eqeq12d ismgmid mpbiri simprd id anbi12d
        oveq2 oveq1 rcla4cva sylan ) AGBMZDNZUJOZUJGDNZUJOZPZBCQZHCRGHDNZHOZHGD
        NZHOZPZAGCRZUPAVBUPPGGOGSABCDGEFGIJKLUAUBUCUOVABHCUJHOZULURUNUTVCUKUQUJ
        HUJHGDUFVCUDZTVCUMUSUJHUJHGDUGVDTUEUHUI $.
        $( [27-Dec-2014] $)
    $}

    ${
      $d x ph $.
      ismgmid2.p $e |- P = ( +g ` G ) $.
      ismgmid2.u $e |- ( ph -> U e. B ) $.
      ismgmid2.l $e |- ( ( ph /\ x e. B ) -> ( U P x ) = x ) $.
      ismgmid2.r $e |- ( ( ph /\ x e. B ) -> ( x P U ) = x ) $.
      $( Show that a given element is the identity element of a magma. $)
      ismgmid2 $p |- ( ph -> U = O ) $=
        ( ve wcel cv co wceq wa wral jca ralrimiva oveq1 eqeq1d anbi12d ralbidv
        wrex oveq2 rcla4ev syl2anc ismgmid mpbi2and eqcomd ) AGEAECOZEBPZDQZUOR
        ZUOEDQZUORZSZBCTZGERKAUTBCAUOCOSUQUSLMUAUBZABCDENFGHIJAUNVANPZUODQZUORZ
        UOVCDQZUORZSZBCTZNCUGKVBVIVANECVCERZVHUTBCVJVEUQVGUSVJVDUPUOVCEUODUCUDV
        JVFURUOVCEUODUHUDUEUFUIUJUKULUM $.
        $( [27-Dec-2014] $)
    $}

    $( The identity element of a monoid belongs to the monoid. $)
    mndidcl $p |- ( G e. Mnd -> O e. B ) $=
      ( vy vx cmnd wcel cplusg cfv eqid mndid mgmidcl ) BHIFABJKZGBCDEOLZFGAOBD
      PMN $.
      $( [27-Dec-2014] $) $( [27-Aug-2011] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d x y O $.  $d x y P $.  $d x X $.
    mndlrid.b $e |- B = ( Base ` G ) $.
    mndlrid.p $e |- P = ( +g ` G ) $.
    mndlrid.o $e |- O = ( 0g ` G ) $.
    $( A monoid's identity element is a two-sided identity. $)
    mndlrid $p |- ( ( G e. Mnd /\ X e. B ) -> ( ( O P X ) = X /\
         ( X P O ) = X ) ) $=
      ( vx vy cmnd wcel mndid mgmlrid ) CKLIABJCDEFHGIJABCFGMN $.
      $( [18-Aug-2011] $)

    $( The identity element of a monoid is a left identity. $)
    mndlid $p |- ( ( G e. Mnd /\ X e. B ) -> ( O P X ) = X ) $=
      ( cmnd wcel wa co wceq mndlrid simpld ) CIJEAJKDEBLEMEDBLEMABCDEFGHNO $.
      $( [18-Aug-2011] $)

    $( The identity element of a monoid is a right identity. $)
    mndrid $p |- ( ( G e. Mnd /\ X e. B ) -> ( X P O ) = X ) $=
      ( cmnd wcel wa co wceq mndlrid simprd ) CIJEAJKDEBLEMEDBLEMABCDEFGHNO $.
      $( [18-Aug-2011] $)
  $}

  ${
    $d x y G $.  $d x ph $.  $d x y Z $.
    grpidd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    grpidd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    grpidd.z $e |- ( ph -> Z e. B ) $.
    grpidd.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    grpidd.j $e |- ( ( ph /\ x e. B ) -> ( x P Z ) = x ) $.
    $( Deduce the identity element of a monoid from its properties. $)
    grpidd $p |- ( ph -> Z = ( 0g ` G ) ) $=
      ( cbs cfv eqid wcel co wceq oveqd eqtr3d syldan cplusg c0g eleqtrd eleq2d
      cv biimpar wa adantr ismgmid2 ) ABELMZEUAMZFEEUBMZUJNULNUKNAFCUJIGUCABUEZ
      UJOZUMCOZFUMUKPZUMQAUOUNACUJUMGUDUFZAUOUGZFUMDPUPUMURDUKFUMADUKQUOHUHZRJS
      TAUNUOUMFUKPZUMQUQURUMFDPUTUMURDUKUMFUSRKSTUI $.
      $( [6-Jan-2015] $)
  $}

  ${
    $d x y z B $.  $d u x y z G $.  $d x y z ph $.  $d u x Z $.
    ismndd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    ismndd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    ismndd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    ismndd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    ismndd.z $e |- ( ph -> Z e. B ) $.
    ismndd.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    ismndd.j $e |- ( ( ph /\ x e. B ) -> ( x P Z ) = x ) $.
    $( Deduce a monoid from its properties. $)
    ismndd $p |- ( ph -> G e. Mnd ) $=
      ( co wcel wceq wa wral cplusg cfv cbs wrex cmnd w3a 3adant3r3 ralrimivvva
      vu cv oveqd eleq12d eqidd oveq123d eqeq12d anbi12d raleqbidv mpbid eleq2d
      eleqtrd biimpar adantr eqtr3d syldan ralrimiva oveq1 eqeq1d oveq2 ralbidv
      jca rcla4ev syl2anc eqid ismnd sylanbrc ) ABUJZCUJZGUAUBZPZGUCUBZQZVSDUJZ
      VRPZVPVQWBVRPZVRPZRZSZDVTTZCVTTZBVTTZUIUJZVPVRPZVPRZVPWKVRPZVPRZSZBVTTZUI
      VTUDZGUEQAVPVQFPZEQZWSWBFPZVPVQWBFPZFPZRZSZDETZCETZBETWJAXEBCDEEEAVPEQZVQ
      EQZWBEQZUFSWTXDAXHXIWTXJKUGLVJUHAXGWIBEVTIAXFWHCEVTIAXEWGDEVTIAWTWAXDWFAW
      SVSEVTAFVRVPVQJUKZIULAXAWCXCWEAWSVSWBWBFVRJXKAWBUMUNAVPVPXBWDFVRJAVPUMAFV
      RVQWBJUKUNUOUPUQUQUQURAHVTQHVPVRPZVPRZVPHVRPZVPRZSZBVTTZWRAHEVTMIUTAXPBVT
      AVPVTQZXHXPAXHXRAEVTVPIUSVAAXHSZXMXOXSHVPFPXLVPXSFVRHVPAFVRRXHJVBZUKNVCXS
      VPHFPXNVPXSFVRVPHXTUKOVCVJVDVEWQXQUIHVTWKHRZWPXPBVTYAWMXMWOXOYAWLXLVPWKHV
      PVRVFVGYAWNXNVPWKHVPVRVHVGUPVIVKVLVTVRUIGBCDVTVMVRVMVNVO $.
      $( [6-Jan-2015] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d x y z P $.
    mndfo.b $e |- B = ( Base ` G ) $.
    mndfo.p $e |- P = ( +g ` G ) $.
    $( The addition operation of a monoid is an onto function (assuming it is a
       function).  (Contributed by Mario Carneiro, 13-Jan-2015.) $)
    mndfo $p |- ( ( G e. Mnd /\ P Fn ( B X. B ) ) ->
      P : ( B X. B ) -onto-> B ) $=
      ( vx vy vz cmnd wcel wa cv co wceq wrex wral simpr adantr sylanbrc eqeq2d
      cxp wfn wf wfo mndcl 3expb ralrimivva ffnov c0g cfv mndidcl mndrid eqcomd
      eqid oveq1 oveq2 rcla42ev syl3anc ralrimiva foov ) CIJZBAAUAZUBZKZVBABUCZ
      FLZGLZHLZBMZNZHAOGAOZFAPZVBABUDVDVCVFVGBMAJZGAPFAPZVEVAVCQVAVNVCVAVMFGAAV
      AVFAJZVGAJVMABCVFVGDEUEUFUGRFGAAABUHSVAVLVCVAVKFAVAVOKZVOCUIUJZAJZVFVFVQB
      MZNZVKVAVOQVAVRVOACVQDVQUNZUKRVPVSVFABCVQVFDEWAULUMVJVTVFVFVHBMZNGHVFVQAA
      VGVFNVIWBVFVGVFVHBUOTVHVQNWBVSVFVHVQVFBUPTUQURUSRGHFAAABUTS $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y F $.  $d x y ph $.  $d y Y $.
    $d x y G $.  $d x y X $.
    $( Lemma for ~ mndpropd . $)
    proplem2 $p |- ( ( ( X e. A /\ Y e. B ) /\
      A. x e. A A. y e. B ( x F y ) e. C ) -> ( X F Y ) e. C ) $=
      ( cv co wcel wceq oveq1 eleq1d oveq2 rcla42va ) AIZBIZFJZEKGHFJZEKGRFJZEK
      ABGHCDQGLSUAEQGRFMNRHLUATERHGFONP $.
      $( [6-Dec-2014] $)

    proplem.1 $e |- ( ( ph /\ ( x e. A /\ y e. B ) ) ->
                         ( x F y ) = ( x G y ) ) $.
    $( Lemma for ~ mndpropd . $)
    proplem $p |- ( ( ph /\ ( X e. A /\ Y e. B ) ) ->
                         ( X F Y ) = ( X G Y ) ) $=
      ( cv co wceq wral wcel wa ralrimivva oveq1 eqeq12d oveq2 rcla42v impcom
      sylan ) ABKZCKZFLZUDUEGLZMZCENBDNZHDOIEOPZHIFLZHIGLZMZAUHBCDEJQUJUIUMUHUM
      HUEFLZHUEGLZMBCHIDEUDHMUFUNUGUOUDHUEFRUDHUEGRSUEIMUNUKUOULUEIHFTUEIHGTSUA
      UBUC $.
      $( [6-Dec-2014] $)
  $}

  ${
    $d s u v w x y z B $.  $d s u v w x y z K $.  $d s u v w x y z ph $.
    $d s u v w x y z L $.
    mndpropd.1 $e |- ( ph -> B = ( Base ` K ) ) $.
    mndpropd.2 $e |- ( ph -> B = ( Base ` L ) ) $.
    mndpropd.3 $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
      ( x ( +g ` K ) y ) = ( x ( +g ` L ) y ) ) $.
    $( If two structures have the same group components (properties), one is a
       monoid iff the other one is.  (Contributed by Mario Carneiro,
       6-Jan-2015.) $)
    mndpropd $p |- ( ph -> ( K e. Mnd <-> L e. Mnd ) ) $=
      ( vu vv vw vs cv co wcel wral wa wceq proplem cplusg cmnd simplr ad2antrr
      cfv cbs simprl eleqtrd simprr eqid syl3anc eleqtrrd ralrimivva ex adantlr
      mndcl eleq12d mpbird wrex simplll simplrl simplrr syl12anc eleq1d simpllr
      wb proplem2 syl21anc simpr oveq1d eqtrd oveq2d eqeq12d ralbidva 2ralbidva
      anbi12d adantr eleq2d anbi1d raleqbidv 3bitr3d rexbidva raleqdv rexeqbidv
      eqeq1d ismnd 3bitr4g pm5.21ndd ) ABNZCNZEUAUEZOZDPZCDQBDQZEUBPZFUBPZAWOWN
      AWORZWMBCDDWQWIDPZWJDPZRZRZWLEUFUEZDXAWOWIXBPWJXBPWLXBPAWOWTUCXAWIDXBWQWR
      WSUGADXBSZWOWTGUDZUHXAWJDXBWQWRWSUIXDUHXBWKEWIWJXBUJZWKUJZUPUKXDULUMUNAWP
      WNAWPRZWMBCDDXGWTRZWMWIWJFUAUEZOZFUFUEZPZXHWPWIXKPWJXKPXLAWPWTUCXHWIDXKXG
      WRWSUGADXKSZWPWTHUDZUHXHWJDXKXGWRWSUIXNUHXKXIFWIWJXKUJZXIUJZUPUKXHWLXJDXK
      AWTWLXJSWPIUOXNUQURUMUNAWNWOWPVFAWNRZJNZKNZWKOZXBPZXTLNZWKOZXRXSYBWKOZWKO
      ZSZRZLXBQZKXBQZJXBQZMNZXRWKOZXRSZXRYKWKOZXRSZRZJXBQZMXBUSZRXRXSXIOZXKPZYS
      YBXIOZXRXSYBXIOZXIOZSZRZLXKQZKXKQZJXKQZYKXRXIOZXRSZXRYKXIOZXRSZRZJXKQZMXK
      USZRWOWPXQYJUUHYRUUOXQXTDPZYFRZLDQZKDQZJDQYSDPZUUDRZLDQZKDQZJDQYJUUHXQUUR
      UVBJKDDXQXRDPZXSDPZRZRZUUQUVALDUVGYBDPZRZUUPUUTYFUUDUVIXTYSDUVIAUVDUVEXTY
      SSAWNUVFUVHUTZXQUVDUVEUVHVAZXQUVDUVEUVHVBZABCDDWKXIXRXSITVCZVDUVIYCUUAYEU
      UCUVIYCXTYBXIOZUUAUVIAUUPUVHYCUVNSUVJUVIUVDUVEWNUUPUVKUVLAWNUVFUVHVEZBCDD
      DWKXRXSVGVHUVGUVHVIZABCDDWKXIXTYBITVCUVIXTYSYBXIUVMVJVKUVIYEXRYDXIOZUUCUV
      IAUVDYDDPZYEUVQSUVJUVKUVIUVEUVHWNUVRUVLUVPUVOBCDDDWKXSYBVGVHABCDDWKXIXRYD
      ITVCUVIYDUUBXRXIUVIAUVEUVHYDUUBSUVJUVLUVPABCDDWKXIXSYBITVCVLVKVMVPVNVOXQU
      USYIJDXBAXCWNGVQZXQUURYHKDXBUVSXQUUQYGLDXBUVSXQUUPYAYFXQDXBXTUVSVRVSVTVTV
      TXQUVCUUGJDXKAXMWNHVQZXQUVBUUFKDXKUVTXQUVAUUELDXKUVTXQUUTYTUUDXQDXKYSUVTV
      RVSVTVTVTWAXQYPJDQZMDUSUUMJDQZMDUSYRUUOXQUWAUWBMDXQYKDPZRZYPUUMJDUWDUVDRZ
      YMUUJYOUULUWEYLUUIXRUWEAUWCUVDYLUUISAWNUWCUVDUTZXQUWCUVDUCZUWDUVDVIZABCDD
      WKXIYKXRITVCWEUWEYNUUKXRUWEAUVDUWCYNUUKSUWFUWHUWGABCDDWKXIXRYKITVCWEVPVNW
      BXQUWAYQMDXBUVSXQYPJDXBUVSWCWDXQUWBUUNMDXKUVTXQUUMJDXKUVTWCWDWAVPXBWKMEJK
      LXEXFWFXKXIMFJKLXOXPWFWGUNWH $.
      $( [6-Jan-2015] $) $( [27-Nov-2014] $)

    grpidpropd.4 $e |- ( ph -> K e. _V ) $.
    grpidpropd.5 $e |- ( ph -> L e. _V ) $.
    $( If two structures have the same group components (properties), they have
       the same identity element. $)
    grpidpropd $p |- ( ph -> ( 0g ` K ) = ( 0g ` L ) ) $=
      ( cv cfv co wceq wa wral crio wcel eqid vz cplusg cbs c0g proplem ancom2s
      vw eqeq1d anbi12d anassrs ralbidva riotabidva raleqdv riotaeqbidv 3eqtr3d
      wb cvv grpidval syl 3eqtr4d ) ABLZCLZEUBMZNZVBOZVBVAVCNZVBOZPZCEUCMZQZBVI
      RZVAVBFUBMZNZVBOZVBVAVLNZVBOZPZCFUCMZQZBVRRZEUDMZFUDMZAVHCDQZBDRVQCDQZBDR
      VKVTAWCWDBDAVADSZPVHVQCDAWEVBDSZVHVQUPAWEWFPPZVEVNVGVPWGVDVMVBIUHWGVFVOVB
      AWFWEVFVOOAUAUGDDVCVLVBVAABCDDVCVLUALUGLIUEUEUFUHUIUJUKULAWCVJBDVIGAVHCDV
      IGUMUNAWDVSBDVRHAVQCDVRHUMUNUOAEUQSWAVKOJCVIVCBEWAUQVITVCTWATURUSAFUQSWBV
      TOKCVRVLBFWBUQVRTVLTWBTURUSUT $.
      $( [27-Nov-2014] $)
  $}

  ${
    $d x y K $.  $d x y L $.
    mndprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    mndprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    $( If two structures have the same group components (properties), one is a
       group iff the other one is. $)
    mndprop $p |- ( K e. Mnd <-> L e. Mnd ) $=
      ( vx vy cmnd wcel wb wtru cbs cfv eqidd wceq a1i cv cplusg co wa oveqi
      mndpropd trud ) AGHBGHIJEFAKLZABJUCMUCBKLNJCOEPZFPZAQLZRUDUEBQLZRNJUDUCHU
      EUCHSSUFUGUDUEDTOUAUB $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d u v w x y B $.  $d u v w x y G $.  $d u v w x y H $.  $d u v w x y P $.
    $d u v w x y S $.  $d u v w x y Z $.
    issubmnd.b $e |- B = ( Base ` G ) $.
    issubmnd.p $e |- P = ( +g ` G ) $.
    issubmnd.z $e |- Z = ( 0g ` G ) $.
    issubmnd.h $e |- H = ( G |`s S ) $.
    $( Characterize a submonoid by closure properties. $)
    issubmnd $p |- ( ( G e. Mnd /\ S C_ B /\ Z e. S ) ->
      ( H e. Mnd <-> A. x e. S A. y e. S ( x P y ) e. S ) ) $=
      ( wcel w3a cv co wa wceq sseld sylan vu vv vw cmnd wss wral simplr simprl
      cbs cfv simpll2 ressbas2 syl eleqtrd simprr eqid ressplusg mndcl eleqtrrd
      syl3anc ralrimivva simpl2 cplusg proplem2 ancoms 3impb 3adant1l 3anim123d
      a1i imp simpl1 mndass syldan simpl3 sselda mndlid mndrid ismndd impbida )
      FUDMZECUEZHEMZNZGUDMZAOZBOZDPZEMZBEUFAEUFZWCWDQZWHABEEWJWEEMZWFEMZQZQZWGG
      UIUJZEWNWDWEWOMWFWOMWGWOMWCWDWMUGWNWEEWOWJWKWLUHWNWAEWORZVTWAWBWDWMUKECGF
      LIULZUMZUNWNWFEWOWJWKWLUOWRUNWODGWEWFWOUPEDFGLJUQZURUTWRUSVAWCWIQZUAUBUCE
      DGHWTWAWPVTWAWBWIVBZWQUMDGVCUJRWTWSVIWIUAOZEMZUBOZEMZXBXDDPZEMZWCWIXCXEXG
      XCXEQWIXGABEEEDXBXDVDVEVFVGWTXCXEUCOZEMZNZXBCMZXDCMZXHCMZNZXFXHDPXBXDXHDP
      DPRZWTXJXNWTXCXKXEXLXIXMWTECXBXASWTECXDXASWTECXHXASVHVJWTVTXNXOVTWAWBWIVK
      ZCDFXBXDXHIJVLTVMVTWAWBWIVNWTXCXKHXBDPXBRZWTECXBXAVOZWTVTXKXQXPCDFHXBIJKV
      PTVMWTXCXKXBHDPXBRZXRWTVTXKXSXPCDFHXBIJKVQTVMVRVS $.
      $( [10-Jan-2015] $)
  $}

  ${
    $d x B $.  $d x G $.  $d x H $.  $d x S $.  $d x Z $.
    submnd0.b $e |- B = ( Base ` G ) $.
    submnd0.z $e |- Z = ( 0g ` G ) $.
    submnd0.h $e |- H = ( G |`s S ) $.
    $( The zero of a submonoid is the same as the zero in the parent monoid.
       (Note that we must add the condition that the zero of the parent monoid
       is actually contained in the submonoid, because it is possible to have
       "subsets that are monoids" which are not submonoids because they have a
       different identity element.) $)
    submnd0 $p |- ( ( ( G e. Mnd /\ H e. Mnd ) /\ ( S C_ B /\ Z e. S ) ) ->
      Z = ( 0g ` H ) ) $=
      ( vx cmnd wcel wa wss cbs cfv cplusg eqid wceq co syl2an ressplusg simprr
      c0g ressbas2 ad2antrl eleqtrd cv simpll ressbasss mndlid mndrid ismgmid2
      sseli ) CJKZDJKZLZBAMZEBKZLZLZIDNOZCPOZEDDUCOZVAQVCQBVBCDHVBQZUAUTEBVAUPU
      QURUBUQBVARUPURBADCHFUDUEUFUTUNIUGZAKZEVEVBSVERVEVAKZUNUOUSUHZVAAVEBADCHF
      UIUMZAVBCEVEFVDGUJTUTUNVFVEEVBSVERVGVHVIAVBCEVEFVDGUKTUL $.
      $( [10-Jan-2015] $)
  $}

  ${
    $d x y B $.  $d x F $.  $d x y I $.  $d x y P $.  $d x y ph $.  $d x y R $.
    $d x G $.  $d x y S $.  $d x y V $.  $d x y W $.  $d x y Y $.  $d y Z $.
    prdmplusgcl.y $e |- Y = ( S Xm_ R ) $.
    prdmplusgcl.b $e |- B = ( Base ` Y ) $.
    prdmplusgcl.p $e |- P = ( +g ` Y ) $.
    prdmplusgcl.s $e |- ( ph -> S e. V ) $.
    prdmplusgcl.i $e |- ( ph -> I e. W ) $.
    prdmplusgcl.r $e |- ( ph -> R : I --> Mnd ) $.
    ${
      prdmplusgcl.f $e |- ( ph -> F e. B ) $.
      prdmplusgcl.g $e |- ( ph -> G e. B ) $.
      $( Structure product pointwise sums are closed when the factors are
         monoids.  (Contributed by Stefan O'Rear, 10-Jan-2015.) $)
      prdmplusgcl $p |- ( ph -> ( F P G ) e. B ) $=
        ( wcel vx co cv cfv cplusg cmpt cmnd wf wfn ffn syl prdmplusgval cbs wa
        wral ffvelrn sylan adantr simpr prdmbasprj eqid mndcl syl3anc ralrimiva
        prdmbasmpt mpbird eqeltrd ) AFGCUBUAHUAUCZFUDZVHGUDZVHDUDZUEUDZUBZUFZBA
        UABCDEFGHIJKLMOPAHUGDUHZDHUIZQHUGDUJUKZRSNULAVNBTVMVKUMUDZTZUAHUOAVSUAH
        AVHHTZUNZVKUGTZVIVRTVJVRTVSAVOVTWBQHUGVHDUPUQWABDEFHVHIJKLMAEITVTOURZAH
        JTVTPURZAVPVTVQURZAFBTVTRURAVTUSZUTWABDEGHVHIJKLMWCWDWEAGBTVTSURWFUTVRV
        LVKVIVJVRVAVLVAVBVCVDAUABDEVMHIJKLMOPVQVEVFVG $.
        $( [10-Jan-2015] $)
    $}

    prdmidlem.z $e |- Z = ( 0g o. R ) $.
    $( Characterization of identity in a structure product. $)
    prdmidlem $p |- ( ph -> ( Z e. B /\ A. x e. B ( ( Z P x ) = x /\
        ( x P Z ) = x ) ) ) $=
      ( vy wcel cv co wceq wral cfv c0g cmpt ccom cvv fvex a1i cmnd feqmptd wfn
      wa fn0g dffn5v sylib fveq2 fmptco syl5eq cbs wf ffvelrn sylan mndidcl syl
      eqid ralrimiva prdmbasmpt mpbird eqeltrd cplusg fveq1i adantr simpr fvco2
      ffn syl2anc adantlr oveq1d ad2antrr simplr prdmbasprj mndlid prdmplusgval
      eqtrd mpteq2dva prdmbasfn 3eqtr4d oveq2d mndrid jca ) AKCTZKBUAZDUBZWOUCZ
      WOKDUBZWOUCZUOZBCUDAKSGSUAZEUEZUFUEZUGZCAKUFEUHZXDRASBGUIXBWOUFUEZXCEUFXB
      UITAXAGTZUOZXAEUJUKASGULEQUMAUFUIUNZUFBUIXFUGUCXIAUPUKBUIUFUQURWOXBUFUSUT
      VAAXDCTXCXBVBUEZTZSGUDAXKSGXHXBULTZXKAGULEVCZXGXLQGULXAEVDZVEXJXBXCXJVHZX
      CVHZVFVGVIASCEFXCGHIJLMOPAXMEGUNZQGULEVRVGZVJVKVLZAWTBCAWOCTZUOZWQWSYASGX
      AKUEZXAWOUEZXBVMUEZUBZUGSGYCUGZWPWOYASGYEYCYAXGUOZYEXCYCYDUBZYCYGYBXCYCYD
      AXGYBXCUCXTXHYBXAXEUEZXCXAKXERVNXHXQXGYIXCUCAXQXGXRVOAXGVPGUFEXAVQVSVAVTZ
      WAYGXLYCXJTZYHYCUCYAXMXGXLAXMXTQVOXNVEZYGCEFWOGXAHIJLMAFHTZXTXGOWBAGITZXT
      XGPWBAXQXTXGXRWBAXTXGWCYAXGVPWDZXJYDXBXCYCXOYDVHZXPWEVSWGWHYASCDEFKWOGHIJ
      LMAYMXTOVOZAYNXTPVOZAXQXTXRVOZAWNXTXSVOZAXTVPZNWFYAWOGUNWOYFUCYACEFWOGHIJ
      LMYQYRYSUUAWISGWOUQURZWJYASGYCYBYDUBZUGYFWRWOYASGUUCYCYGUUCYCXCYDUBZYCYGY
      BXCYCYDYJWKYGXLYKUUDYCUCYLYOXJYDXBXCYCXOYPXPWLVSWGWHYASCDEFWOKGHIJLMYQYRY
      SUUAYTNWFUUBWJWMVIWM $.
      $( [10-Jan-2015] $)
  $}

  ${
    $d a b y I $.  $d a b c y ph $.  $d a b y R $.  $d a b c y Y $.
    $d a b y S $.
    prdmmndd.y $e |- Y = ( S Xm_ R ) $.
    prdmmndd.i $e |- ( ph -> I e. W ) $.
    prdmmndd.s $e |- ( ph -> S e. V ) $.
    prdmmndd.r $e |- ( ph -> R : I --> Mnd ) $.
    $( The product of a family of monoids is a monoid.  (Contributed by Stefan
       O'Rear, 10-Jan-2015.) $)
    prdmmndd $p |- ( ph -> Y e. Mnd ) $=
      ( va vy cfv wcel co wa cvv eqid adantr vb vc cbs cplusg c0g ccom eqidd cv
      elex syl cmnd simprl simprr prdmplusgcl 3impb w3a cmpt wceq ffvelrn sylan
      wf adantlr ad2antrr wfn simplr1 simpr prdmbasprj simplr2 simplr3 syl13anc
      mndass prdmplusgfval oveq1d oveq2d mpteq2dva 3adantr3 simpr3 prdmplusgval
      ffn 3eqtr4d simpr1 simpr2 wral prdmidlem simpld simprd r19.21bi ismndd )
      ALUAUBGUCNZGUDNZGUEBUFZAWIUGAWJUGALUHZWIOZUAUHZWIOZWLWNWJPZWIOZAWMWOQZQWI
      WJBCWLWNDRRGHWISZWJSZACROZWRACEOXAJCEUIUJZTADROZWRADFOXCIDFUIUJZTADUKBVAZ
      WRKTAWMWOULAWMWOUMUNZUOAWMWOUBUHZWIOZUPZQZMDMUHZWPNZXKXGNZXKBNZUDNZPZUQMD
      XKWLNZXKWNXGWJPZNZXOPZUQWPXGWJPWLXRWJPXJMDXPXTXJXKDOZQZXQXKWNNZXOPZXMXOPZ
      XQYCXMXOPZXOPZXPXTYBXNUKOZXQXNUCNZOYCYIOXMYIOYEYGURAYAYHXIAXEYAYHKDUKXKBU
      SUTVBYBWIBCWLDXKRRGHWSAXAXIYAXBVCZAXCXIYAXDVCZABDVDZXIYAAXEYLKDUKBVSUJZVC
      ZWMWOXHAYAVEZXJYAVFZVGYBWIBCWNDXKRRGHWSYJYKYNWMWOXHAYAVHZYPVGYBWIBCXGDXKR
      RGHWSYJYKYNWMWOXHAYAVIZYPVGYIXOXNXQYCXMYISXOSVKVJYBXLYDXMXOYBWIWJBCWLWNDX
      KRRGHWSYJYKYNYOYQWTYPVLVMYBXSYFXQXOYBWIWJBCWNXGDXKRRGHWSYJYKYNYQYRWTYPVLV
      NVTVOXJMWIWJBCWPXGDRRGHWSAXAXIXBTZAXCXIXDTZAYLXIYMTZAWMWOWQXHXFVPAWMWOXHV
      QZWTVRXJMWIWJBCWLXRDRRGHWSYSYTUUAAWMWOXHWAXJWIWJBCWNXGDRRGHWSWTYSYTAXEXIK
      TAWMWOXHWBUUBUNWTVRVTAWKWIOZWKWLWJPWLURZWLWKWJPWLURZQZLWIWCZALWIWJBCDRRGW
      KHWSWTXBXDKWKSWDZWEAWMQZUUDUUEAUUFLWIAUUCUUGUUHWFWGZWEUUIUUDUUEUUJWFWH $.
      $( [10-Jan-2015] $)

    $( Zero in a product of monoids.  (Contributed by Stefan O'Rear,
       10-Jan-2015.) $)
    prdm0g $p |- ( ph -> ( 0g o. R ) = ( 0g ` Y ) ) $=
      ( vb va cfv wcel co wceq wa cvv eqid c0g ccom cbs cv cplusg wral elex syl
      prdmidlem cmnd wrex prdmmndd mndid ismgmid mpbid eqcomd ) AGUANZUABUBZAUR
      GUCNZOURLUDZGUENZPUTQUTURVAPUTQRLUSUFRUQURQALUSVABCDSSGURHUSTZVATZACEOCSO
      JCEUGUHADFODSOIDFUGUHKURTUIALUSVAURMGUQVBUQTVCAGUJOMUDZUTVAPUTQUTVDVAPUTQ
      RLUSUFMUSUKABCDEFGHIJKULLMUSVAGVBVCUMUHUNUOUP $.
      $( [10-Jan-2015] $)
  $}

  ${
    $d x I $.  $d r x R $.  $d x V $.  $d r x Z $.
    pwsmnd.y $e |- Y = ( R ^s I ) $.
    $( The structure power of a monoid is a monoid.  (Contributed by Mario
       Carneiro, 11-Jan-2015.) $)
    pwsmnd $p |- ( ( R e. Mnd /\ I e. V ) -> Y e. Mnd ) $=
      ( cmnd wcel wa csca cfv csn cxp cprdm co eqid pwsval cvv simpr fvex wf
      a1i wss fconstg snssi fss syl2anc adantr prdmmndd eqeltrd ) AFGZBCGZHZDAI
      JZBAKZLZMNZFAUMBFCDEUMOPULUOUMBQCUPUPOUJUKRUMQGULAISUAUJBFUOTZUKUJBUNUOTU
      NFUBUQBAFUCAFUDBUNFUOUEUFUGUHUI $.
      $( [11-Jan-2015] $)

    pws0g.z $e |- Z = ( 0g ` R ) $.
    $( Zero in a product of monoids.  (Contributed by Mario Carneiro,
       11-Jan-2015.) $)
    pws0g $p |- ( ( R e. Mnd /\ I e. V ) -> ( I X. { Z } ) = ( 0g ` Y ) ) $=
      ( vx vr cmnd wcel c0g csn cxp csca cfv cvv a1i cmpt wceq wa ccom cprdm co
      eqid simpr fvex wss fconstg snssi fss syl2anc adantr prdm0g elex ad2antrr
      wf cv fconstmpt wfn fn0g dffn5v sylib fveq2 fmptco syl6reqr pwsval fveq2d
      syl6eqr 3eqtr4d ) AJKZBCKZUAZLBAMZNZUBZAOPZVOUCUDZLPBEMNZDLPVMVOVQBQCVRVR
      UEVKVLUFVQQKVMAOUGRVKBJVOUQZVLVKBVNVOUQVNJUHVTBAJUIAJUJBVNJVOUKULUMUNVMVP
      HBESVSVMHIBQAIURZLPZEVOLVKAQKVLHURBKAJUOUPVOHBASTVMHBAUSRVMLQUTZLIQWBSTWC
      VMVARIQLVBVCWAATWBALPEWAALVDGVIVEHBEUSVFVMDVRLAVQBJCDFVQUEVGVHVJ $.
      $( [11-Jan-2015] $)
  $}

