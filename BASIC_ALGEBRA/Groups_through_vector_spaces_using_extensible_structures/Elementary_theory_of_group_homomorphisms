$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Elementary theory of group homomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c GrpHom $.

  $( Extend class notation with the generator of group hom-sets. $)
  cghm $a class GrpHom $.

  ${
    $d g s t w x y $.
    $( A homomorphism of groups is a map between two structures which preserves
       the group operation.  Requiring both sides to be groups simplifies most
       theorems at the cost of complicating the theorem which pushes forward a
       group structure. $)
    df-ghm $a |- GrpHom = ( s e. Grp , t e. Grp |-> { g |
        [ ( Base ` s ) / w ] ( g : w --> ( Base ` t ) /\
            A. x e. w A. y e. w ( g ` ( x ( +g ` s ) y ) ) =
                ( ( g ` x ) ( +g ` t ) ( g ` y ) ) ) } ) $.

    $( Lemma for group homomorphisms. $)
    reldmghm $p |- Rel dom GrpHom $=
      ( vs vt vw vg vx vy cgrp cv cbs cfv wf cplusg co wceq wral wa wsbc df-ghm
      cab cghm reldmmpt2 ) ABGGCHZBHZIJDHZKEHZFHZAHZLJMUDJUEUDJUFUDJUCLJMNFUBOE
      UBOPCUGIJQDSTEFCBDARUA $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d s t w u v f S $.  $d s t w u v f T $.  $d u v f t s X $.
    $d u v f s t A $.  $d u v f s t Y $.  $d u v f s t B $.  $d F f u v $.
    isghm.w $e |- X = ( Base ` S ) $.
    isghm.x $e |- Y = ( Base ` T ) $.
    isghm.a $e |- A = ( +g ` S ) $.
    isghm.b $e |- B = ( +g ` T ) $.
    $( Property of being a homomorphism of groups.  (Contributed by Stefan
       O'Rear, 31-Dec-2014.) $)
    isghm $p |- ( T e. W -> ( F e. ( S GrpHom T ) <->
        ( ( S e. Grp /\ T e. Grp ) /\ ( F : X --> Y /\ A. u e. X A. v e. X
              ( F ` ( u A v ) ) = ( ( F ` u ) B ( F ` v ) ) ) ) ) ) $=
      ( vf wcel cvv cgrp cfv wral vs vt vw cghm co wa wf cv wceq wb cop cxp cdm
      elex eleq2i biimpi elfvdm cbs cplusg wsbc cab df-ghm fvex feq2 raleqbi1dv
      df-ov raleq anbi12d sbcie abbii fabex dmmpt2 3syl opelxpg syl5ib pm4.71rd
      fveq2 syl6eqr feq2d fveq2d eqeq1d raleqbidv syl5bb abbidv feq3 syl eqeq2d
      oveqd 2ralbidv eqeltri mapex mp2an simpl ss2abi ssexi ovmpt2 eleq2d mpan2
      fex adantr feq1 fveq1 oveq12d eqeq12d elab3 syl6bb pm5.32i ) FHPFQPZGEFUD
      UEZPZERPFRPUFZIJGUGZBUHZAUHZCUEZGSZXMGSZXNGSZDUEZUIZAITBITZUFZUFZUJFHUNXH
      XJXKXJUFYCXHXJXKXJEFUKZRRULZPZXHXKXJGYDUDSZPZYDUDUMZPZYFXJYHXIYGGEFUDVFUO
      UPGYDUDUQYJYFYIYEYDUAUBRRUCUHZUBUHZURSZOUHZUGZXMXNUAUHZUSSZUEZYNSZXMYNSZX
      NYNSZYLUSSZUEZUIZAYKTZBYKTZUFZUCYPURSZUTZOVAZUDBAUCUBOUAVBZUUDAUUHTZBUUHT
      ZOUUHYMUUJYPURVCZYLURVCUUIUUHYMYNUGZUUMUFZOUUGUUPUCUUHUUNYKUUHUIYOUUOUUFU
      UMYKUUHYMYNVDUUEUULBYKUUHUUDAYKUUHVGVEVHVIZVJVKVLUOUPVMEFRRQVNVOVPXKXJYBX
      KXJGIJYNUGZXOYNSZYTUUADUEZUIZAITBITZUFZOVAZPYBXKXIUVDGUAUBEFRRUUJUVDUDIYM
      YNUGZUUSUUCUIZAITZBITZUFZOVAYPEUIZUUIUVIOUUIUUPUVJUVIUUQUVJUUOUVEUUMUVHUV
      JUUHIYMYNUVJUUHEURSZIYPEURVQKVRZVSUVJUULUVGBUUHIUVLUVJUUDUVFAUUHIUVLUVJYS
      UUSUUCUVJYRXOYNUVJYQCXMXNUVJYQEUSSCYPEUSVQMVRWHVTWAWBWBVHWCWDYLFUIZUVIUVC
      OUVMUVEUURUVHUVBUVMYMJUIUVEUURUJUVMYMFURSZJYLFURVQLVRYMJIYNWEWFUVMUVFUVAB
      AIIUVMUUCUUTUUSUVMUUBDYTUUAUVMUUBFUSSDYLFUSVQNVRWHWGWIVHWDUUKUVDUUROVAZIQ
      PZJQPUVOQPIUVKQKEURVCWJZJUVNQLFURVCWJIJQQOWKWLUVCUUROUURUVBWMWNWOWPWQUVCY
      BOGXLGQPZYAXLUVPUVRUVQIJQGWSWRWTYNGUIZUURXLUVBYAIJYNGXAUVSUVAXTBAIIUVSUUS
      XPUUTXSXOYNGXBUVSYTXQUUAXRDXMYNGXBXNYNGXBXCXDWIVHXEXFXGXFWF $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d X a b $.  $d Y a b $.  $d T a b $.  $d S a b $.  $d V a b $.
    $d F a b $.
    isghm2.x $e |- X = ( Base ` S ) $.
    isghm2.y $e |- Y = ( Base ` T ) $.
    $( Non-quantified consequences of a group homomorphism.  (Contributed by
       Stefan O'Rear, 31-Dec-2014.) $)
    isghm2 $p |- ( ( F e. ( S GrpHom T ) /\ T e. V ) -> ( S e. Grp /\ T e. Grp
        /\ F : X --> Y ) ) $=
      ( va vb wcel cghm co cgrp wa cv cplusg cfv wral eqid w3a wceq isghm 3expa
      wf id1 adantrr syl6bi impcom ) BDKZCABLMKZANKZBNKZEFCUEZUAZUJUKULUMOZUNIP
      ZJPZAQRZMCRUQCRURCRBQRZMUBJESIESZOOUOJIUSUTABCDEFGHUSTUTTUCUPUNUOVAULUMUN
      UOUOUFUDUGUHUI $.
      $( [31-Dec-2014] $)

    $( A group homomorphism is a function.  (Contributed by Stefan O'Rear,
       31-Dec-2014.) $)
    ghmf $p |- ( ( F e. ( S GrpHom T ) /\ T e. V ) -> F : X --> Y ) $=
      ( cghm co wcel wa cgrp wf isghm2 simp3d ) CABIJKBDKLAMKBMKEFCNABCDEFGHOP
      $.
      $( [31-Dec-2014] $)
  $}

  ${
    $( A group homomorphism is only defined when the domain is a group.
       (Contributed by Stefan O'Rear, 31-Dec-2014.) $)
    ghmgrp1 $p |- ( F e. ( S GrpHom T ) -> S e. Grp ) $=
      ( cghm co wcel cvv cgrp wa cbs cfv wf eqid isghm2 simp1d wn ovprc2 eleq2d
      c0 noel reldmghm ovprc1 mtbiri con4i mpdan syl6bi impcom pm2.61dan ) CABD
      EZFZBGFZAHFZUJUKIULBHFAJKZBJKZCLABCGUMUNUMMZUNMNOUKPZUJULUPUJCAADEZFZULUP
      UIUQCABDQRURAGFZULUSURUSPZURCSFCTUTUQSCAADUAUBRUCUDURUSIULULUMUMCLAACGUMU
      MUOUONOUEUFUGUH $.
      $( [31-Dec-2014] $)

    $( A group homomorphism is only defined when the codomain is a group.
       (Contributed by Stefan O'Rear, 31-Dec-2014.) $)
    ghmgrp2 $p |- ( ( F e. ( S GrpHom T ) /\ T e. V ) -> T e. Grp ) $=
      ( cghm co wcel wa cgrp cbs cfv wf eqid isghm2 simp2d ) CABEFGBDGHAIGBIGAJ
      KZBJKZCLABCDPQPMQMNO $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d ph a b $.  $d U a b $.  $d V a b $.  $d F a b $.  $d S a b $.
    $d T a b $.  $d A a b $.  $d B a b $.  $d X a b $.
    ghmlin.x $e |- X = ( Base ` S ) $.
    ghmlin.a $e |- A = ( +g ` S ) $.
    ghmlin.b $e |- B = ( +g ` T ) $.
    $( A homomorphism of groups is linear.  (Contributed by Stefan O'Rear,
       31-Dec-2014.) $)
    ghmlin $p |- ( ( ( F e. ( S GrpHom T ) /\ T e. W ) /\
          ( U e. X /\ V e. X ) ) -> ( F ` ( U A V ) ) =
          ( ( F ` U ) B ( F ` V ) ) ) $=
      ( va vb co wcel wa cv cfv wceq cghm wral cbs wf cgrp isghm biimpac simprd
      eqid oveq1 fveq2d fveq2 oveq1d eqeq12d oveq2 oveq2d rcla42v mpan9 ) FCDUA
      OPZDHPZQZMRZNRZAOZFSZVBFSZVCFSZBOZTZNIUBMIUBZEIPGIPQEGAOZFSZEFSZGFSZBOZTZ
      VAIDUCSZFUDZVJVACUEPDUEPQZVRVJQZUTUSVSVTQNMABCDFHIVQJVQUIKLUFUGUHUHVIVPEV
      CAOZFSZVMVGBOZTMNEGIIVBETZVEWBVHWCWDVDWAFVBEVCAUJUKWDVFVMVGBVBEFULUMUNVCG
      TZWBVLWCVOWEWAVKFVCGEAUOUKWEVGVNVMBVCGFULUPUNUQUR $.
      $( [31-Dec-2014] $)
  $}

  ${
    ghmid.y $e |- Y = ( 0g ` S ) $.
    ghmid.z $e |- Z = ( 0g ` T ) $.
    $( A homomorphism of groups preserves the identity.  (Contributed by Stefan
       O'Rear, 31-Dec-2014.) $)
    ghmid $p |- ( ( F e. ( S GrpHom T ) /\ T e. V ) -> ( F ` Y ) = Z ) $=
      ( cghm co wcel wa cfv cplusg wceq cbs simpl cgrp eqid syl2anc ghmgrp1 syl
      simpr adantr grpidcl ghmlin syl22anc grplid fveq2d eqtr3d wb ghmgrp2 ghmf
      wf ffvelrn grpid mpbid eqcomd ) CABIJKZBDKZLZFECMZVAVBVBBNMZJZVBOZFVBOZVA
      EEANMZJZCMZVDVBVAUSUTEAPMZKZVKVIVDOUSUTQUSUTUCVAARKZVKUSVLUTABCUAUDZVJAEV
      JSZGUEUBZVOVGVCABECEDVJVNVGSZVCSZUFUGVAVHECVAVLVKVHEOVMVOVJVGAEEVNVPGUHTU
      IUJVABRKVBBPMZKZVEVFUKABCDULVAVJVRCUNVKVSABCDVJVRVNVRSZUMVOVJVRECUOTVRVCB
      FVBVTVQHUPTUQUR $.
      $( [31-Dec-2014] $)
  $}

  ${
    ghminv.b $e |- B = ( Base ` S ) $.
    ghminv.y $e |- M = ( invg ` S ) $.
    ghminv.z $e |- N = ( invg ` T ) $.
    $( A homomorphism of groups preserves inverses.  (Contributed by Stefan
       O'Rear, 31-Dec-2014.) $)
    ghminv $p |- ( ( F e. ( S GrpHom T ) /\ T e. V /\ X e. B ) ->
        ( F ` ( M ` X ) ) = ( N ` ( F ` X ) ) ) $=
      ( co wcel cfv wceq cplusg c0g eqid syl2anc 3adant3 cghm w3a cgrp 3ad2ant1
      ghmgrp1 simp3 grprinv fveq2d simp1 simp2 grpinvcl ghmlin syl22anc 3eqtr3d
      ghmid cbs wb ghmgrp2 wf ghmf ffvelrn grpinvid1 syl3anc mpbird eqcomd ) DB
      CUALMZCGMZHAMZUBZHDNZFNZHENZDNZVIVKVMOZVJVMCPNZLZCQNZOZVIHVLBPNZLZDNZBQNZ
      DNZVPVQVIVTWBDVIBUCMZVHVTWBOVFVGWDVHBCDUEUDZVFVGVHUFZAVSBEWBHIVSRZWBRZJUG
      SUHVIVFVGVHVLAMZWAVPOVFVGVHUIVFVGVHUJWFVIWDVHWIWEWFABEHIJUKSZVSVOBCHDVLGA
      IWGVORZULUMVFVGWCVQOVHBCDGWBVQWHVQRZUOTUNVICUCMZVJCUPNZMZVMWNMZVNVRUQVFVG
      WMVHBCDGURTVIAWNDUSZVHWOVFVGWQVHBCDGAWNIWNRZUTTZWFAWNHDVASVIWQWIWPWSWJAWN
      VLDVASWNVOCFVQVJVMWRWKWLKVBVCVDVE $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d F a b c $.  $d S a b c $.  $d T a b c $.  $d V a b c $.
    $( Range of a homomorphism is a subgroup.  (Contributed by Stefan O'Rear,
       31-Dec-2014.) $)
    ghmrn $p |- ( ( F e. ( S GrpHom T ) /\ T e. V ) -> ran F e. ( SubGrp ` T )
        ) $=
      ( va vb vc co wcel wa cfv c0 wne cv wral eqid syl wceq adantr wb cghm crn
      csubg cbs wss cplusg cminusg ghmf frn cdm fdm cgrp ghmgrp1 grpbn0 eqnetrd
      dm0rn0 necon3bii sylib ghmlin wfn ffn grpcl 3expb sylan fnfvelrn eqeltrrd
      syl2anc expr ralrimiv oveq2 eleq1d ralrn mpbird ghminv 3expa grpinvcl jca
      wf ralrimiva oveq1 ralbidv fveq2 anbi12d w3a ghmgrp2 issubg2 mpbir3and )
      CABUAHIZBDIZJZCUBZBUCKIZWKBUDKZUEZWKLMZENZFNZBUFKZHZWKIZFWKOZWPBUGKZKZWKI
      ZJZEWKOZWJAUDKZWMCVRZWNABCDXGWMXGPZWMPZUHZXGWMCUIQWJCUJZLMWOWJXLXGLWJXHXL
      XGRXKXGWMCUKQWJAULIZXGLMWHXMWIABCUMSZXGAXIUNQUOXLLWKLCUPUQURWJXFGNZCKZWQW
      RHZWKIZFWKOZXPXBKZWKIZJZGXGOZWJYBGXGWJXOXGIZJZXSYAYEXSXPWPCKZWRHZWKIZEXGO
      ZYEYHEXGWJYDWPXGIZYHWJYDYJJZJZXOWPAUFKZHZCKZYGWKYMWRABXOCWPDXGXIYMPZWRPZU
      SYLCXGUTZYNXGIZYOWKIWJYRYKWJXHYRXKXGWMCVAQZSWJXMYKYSXNXMYDYJYSXGYMAXOWPXI
      YPVBVCVDXGYNCVEVGVFVHVIWJXSYITZYDWJYRUUAYTXRYHFEXGCWQYFRXQYGWKWQYFXPWRVJV
      KVLQSVMYEXOAUGKZKZCKZXTWKWHWIYDUUDXTRXGABCUUBXBDXOXIUUBPZXBPZVNVOYEYRUUCX
      GIZUUDWKIWJYRYDYTSWJXMYDUUGXNXGAUUBXOXIUUEVPVDXGUUCCVEVGVFVQVSWJYRXFYCTYT
      XEYBEGXGCWPXPRZXAXSXDYAUUHWTXRFWKUUHWSXQWKWPXPWQWRVTVKWAUUHXCXTWKWPXPXBWB
      VKWCVLQVMWJBULIWLWNWOXFWDTABCDWEEFWMWRWKBXBXJYQUUFWFQWG $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d B a b $.  $d G a b $.
    idghm.b $e |- B = ( Base ` G ) $.
    $( The identity homomorphism on a group.  (Contributed by Stefan O'Rear,
       31-Dec-2014.) $)
    idghm $p |- ( G e. Grp -> ( _I |` B ) e. ( G GrpHom G ) ) $=
      ( va vb cgrp wcel cid cres cghm co wa wf cv cplusg cfv wceq wral fvresi
      id jca eqid grpcl 3expb oveqan12d adantl eqtr4d ralrimivva wf1o f1oi f1of
      syl ax-mp jctil isghm mpbir2and ) BFGZHAIZBBJKGUQUQLAAURMZDNZENZBOPZKZURP
      ZUTURPZVAURPZVBKZQZEARDARZLUQUQUQUQTZVJUAUQVIUSUQVHDEAAUQUTAGZVAAGZLZLZVD
      VCVGVNVCAGZVDVCQUQVKVLVOAVBBUTVACVBUBZUCUDAVCSULVMVGVCQUQVKVLVEUTVFVAVBAU
      TSAVASUEUFUGUHAAURUIUSAUJAAURUKUMUNEDVBVBBBURFAACCVPVPUOUP $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d S a b $.  $d X a b $.  $d F a b $.  $d T a b $.  $d V a b $.
    $d U a b $.
    resghm.u $e |- U = ( S |`s X ) $.
    $( Restriction of a homomorphism to a subgroup.  (Contributed by Stefan
       O'Rear, 31-Dec-2014.) $)
    resghm $p |- ( ( F e. ( S GrpHom T ) /\ X e. ( SubGrp ` S ) /\ T e. V ) ->
        ( F |` X ) e. ( U GrpHom T ) ) $=
      ( va vb cghm co wcel cfv wa cbs wf wceq eqid syl fvres csubg cres cgrp cv
      w3a cplusg subggrp 3ad2ant2 ghmgrp2 3adant2 jca wss subgss fssres syl2anc
      wral ghmf ressbas2 feq2d mpbid eleq2 anbi12d biimpar 3simpb sseld anim12d
      wb adantr imp ghmlin subgcl 3ad2antl2 oveqan12d adantl 3eqtr4d ralrimivva
      3expb syldan ressplusg isghm 3ad2ant3 mpbir2and ) DABJKLZFAUAMLZBELZUEZDF
      UBZCBJKLZCUCLZBUCLZNZCOMZBOMZWGPZHUDZIUDZAUFMZKZWGMZWOWGMZWPWGMZBUFMZKZQZ
      IWLUPHWLUPZNZWFWIWJWDWCWIWEFACGUGUHWCWEWJWDABDEUIUJUKWFWNXEWFFWMWGPZWNWFA
      OMZWMDPZFXHULZXGWCWEXIWDABDEXHWMXHRZWMRZUQUJWDWCXJWEXHFAXKUMUHZXHWMFDUNUO
      WFFWLWMWGWFXJFWLQZXMFXHCAGXKURSZUSUTWFXDHIWLWLWFWOWLLZWPWLLZNZWOFLZWPFLZN
      ZXDWFYAXRWFXNYAXRVGXOXNXSXPXTXQFWLWOVAFWLWPVAVBSVCWFYANZWRDMZWODMZWPDMZXB
      KZWSXCYBWCWENZWOXHLZWPXHLZNZYCYFQWFYGYAWCWDWEVDVHWFYAYJWFXSYHXTYIWFFXHWOX
      MVEWFFXHWPXMVEVFVIWQXBABWODWPEXHXKWQRZXBRZVJUOYBWRFLZWSYCQWDWCYAYMWEWDXSX
      TYMWQFAWOWPYKVKVQVLWRFDTSYAXCYFQWFXSXTWTYDXAYEXBWOFDTWPFDTVMVNVOVRVPUKWEW
      CWHWKXFNVGWDIHWQXBCBWGEWLWMWLRXLFWQACGYKVSYLVTWAWB $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d x y F $.  $d x y S $.  $d x y T $.  $d x y U $.  $d x y X $.
    resghm2.u $e |- U = ( T |`s X ) $.
    $( One direction of ~ resghm2b .  (Contributed by Mario Carneiro,
       13-Jan-2015.) $)
    resghm2 $p |- ( ( F e. ( S GrpHom U ) /\ X e. ( SubGrp ` T ) ) ->
      F e. ( S GrpHom T ) ) $=
      ( vx vy cghm co wcel cfv wa cgrp cbs wf cv cvv eqid sylancl csubg ghmgrp1
      cplusg wceq wral adantr subgrcl adantl jca wss simpl cress ovex ressbasss
      eqeltri ghmf fss ressplusg ghmlin ralrimivva wb isghm syl mpbir2and ) DAC
      IJKZEBUALKZMZDABIJKZANKZBNKZMZAOLZBOLZDPZGQZHQZAUCLZJDLVODLVPDLBUCLZJUDZH
      VLUEGVLUEZMZVGVIVJVEVIVFACDUBUFVFVJVEEBUGUHZUIVGVNVTVGVLCOLZDPZWCVMUJVNVG
      VECRKZWDVEVFUKZCBEULJRFBEULUMUOZACDRVLWCVLSZWCSUPTEVMCBFVMSZUNVLWCVMDUQTV
      GVEWEVTWFWGVEWEMVSGHVLVLVQVRACVODVPRVLWHVQSZEVRBCFVRSZURUSUTTUIVGVJVHVKWA
      MVAWBHGVQVRABDNVLVMWHWIWJWKVBVCVD $.
      $( [13-Jan-2015] $)

    $( Restriction of a the codomain of a homomorphism.  (Contributed by Mario
       Carneiro, 13-Jan-2015.) $)
    resghm2b $p |- ( ( X e. ( SubGrp ` T ) /\ ran F C_ X ) ->
      ( F e. ( S GrpHom T ) <-> F e. ( S GrpHom U ) ) ) $=
      ( vx vy cfv wcel wss wa co cgrp cbs wf ad2antrr jca eqid syl csubg crn cv
      cghm cplusg wceq wral ghmgrp1 adantl subggrp wfn subgrcl ghmf syl2anc ffn
      simpr simplr df-f sylanbrc wb subgss ressbas2 feq3 mpbid sylan ralrimivva
      ghmlin ressplusg isghm mpbir2and resghm2 ancoms adantlr impbida ) EBUAIJZ
      DUBEKZLZDABUDMJZDACUDMJZVQVRLZVSANJZCNJZLZAOIZCOIZDPZGUCZHUCZAUEIZMDIWGDI
      WHDIBUEIZMUFZHWDUGGWDUGZLZVTWAWBVRWAVQABDUHUIVOWBVPVREBCFUJQZRVTWFWLVTWDE
      DPZWFVTDWDUKZVPWOVTWDBOIZDPZWPVTVRBNJZWRVQVRUPZVOWSVPVREBULQZABDNWDWQWDSZ
      WQSZUMUNWDWQDUOTVOVPVRUQWDEDURUSVTEWEUFZWOWFUTVTEWQKZXDVOXEVPVRWQEBXCVAQE
      WQCBFXCVBTEWEWDDVCTVDVTWKGHWDWDVTVRWSLWGWDJWHWDJLWKVTVRWSWTXARWIWJABWGDWH
      NWDXBWISZWJSZVGVEVFRVTWBVSWCWMLUTWNHGWIWJACDNWDWEXBWESXFEWJBCFXGVHVITVJVO
      VSVRVPVSVOVRABCDEFVKVLVMVN $.
      $( [13-Jan-2015] $)
  $}

  ${
    $( The image of a subgroup under a homomorphism.  (Contributed by Stefan
       O'Rear, 31-Dec-2014.) $)
    ghmima $p |- ( ( F e. ( S GrpHom T ) /\ U e. ( SubGrp ` S ) /\ T e. V ) ->
        ( F " U ) e. ( SubGrp ` T ) ) $=
      ( cghm co wcel csubg cfv w3a cima cres crn df-ima cress eqid resghm simp3
      ghmrn syl2anc syl5eqel ) DABFGHZCAIJHZBEHZKZDCLDCMZNZBIJZDCOUFUGACPGZBFGH
      UEUHUIHABUJDECUJQRUCUDUESUJBUGETUAUB $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d F a b $.  $d V a b $.  $d T a b $.  $d S a b $.
    $( The inverse image of a subgroup under a homomorphism.  (Contributed by
       Stefan O'Rear, 31-Dec-2014.) $)
    ghmpreima $p |- ( ( F e. ( S GrpHom T ) /\ V e. ( SubGrp ` T ) ) ->
        ( `' F " V ) e. ( SubGrp ` S ) ) $=
      ( va vb co wcel csubg cfv wa wceq cvv syl eqid adantr eqeltrd wb elpreima
      mpbir2and cghm ccnv cima cbs wss c0 wne cv cplusg cminusg cdm cnvimass wf
      wral elfvdm elex ghmf fdm syl5sseq c0g cgrp ghmgrp1 grpidcl ghmid subg0cl
      sylan2 adantl wfn ffn ne0i ad2antrr simprll simprrl grpcl simpll ad2antlr
      syl3anc ghmlin syl22anc simplr simprlr subgcl expr sylbid ralrimiv simprl
      simprrr grpinvcl syl2anc ghminv subginvcl ad2ant2l jca issubg2 mpbir3and
      ex w3a ) CABUAGHZDBIJHZKZCUBDUCZAIJHZXAAUDJZUEZXAUFUGZEUHZFUHZAUIJZGZXAHZ
      FXAUNZXFAUJJZJZXAHZKZEXAUNZWTCUKZXAXCCDULWTXCBUDJZCUMZXQXCLWSWRBMHZXSWSBI
      UKZHXTDBIUOBYAUPNZABCMXCXRXCOZXROUQVFZXCXRCURNUSWTAUTJZXAHZXEWTYFYEXCHZYE
      CJZDHZWTAVAHZYGWRYJWSABCVBZPZXCAYEYCYEOZVCNWTYHBUTJZDWSWRXTYHYNLYBABCMYEY
      NYMYNOZVDVFWSYNDHWRDBYNYOVEVGQWTCXCVHZYFYGYIKRWTXSYPYDXCXRCVINZXCYEDCSNTX
      AYEVJNWTXOEXAWTXFXAHZXFXCHZXFCJZDHZKZXOWTYPYRUUBRYQXCXFDCSNWTUUBXOWTUUBKZ
      XKXNUUCXJFXAUUCXGXAHZXGXCHZXGCJZDHZKZXJWTUUDUUHRZUUBWTYPUUIYQXCXGDCSNPWTU
      UBUUHXJWTUUBUUHKZKZXJXIXCHZXICJZDHZUUKYJYSUUEUULWRYJWSUUJYKVKWTYSUUAUUHVL
      ZWTUUBUUEUUGVMZXCXHAXFXGYCXHOZVNVQUUKUUMYTUUFBUIJZGZDUUKWRXTYSUUEUUMUUSLW
      RWSUUJVOWSXTWRUUJYBVPUUOUUPXHUURABXFCXGMXCYCUUQUUROZVRVSUUKWSUUAUUGUUSDHW
      RWSUUJVTWTYSUUAUUHWAWTUUBUUEUUGWGUURDBYTUUFUUTWBVQQWTXJUULUUNKRZUUJWTYPUV
      AYQXCXIDCSNPTWCWDWEUUCXNXMXCHZXMCJZDHZUUCYJYSUVBWTYJUUBYLPWTYSUUAWFZXCAXL
      XFYCXLOZWHWIUUCUVCYTBUJJZJZDUUCWRXTYSUVCUVHLWRWSUUBVOWSXTWRUUBYBVPUVEXCAB
      CXLUVGMXFYCUVFUVGOZWJVQWSUUAUVHDHWRYSDBUVGYTUVIWKWLQWTXNUVBUVDKRZUUBWTYPU
      VJYQXCXMDCSNPTWMWPWDWEWTYJXBXDXEXPWQRYLEFXCXHXAAXLYCUUQUVFWNNWO $.
      $( [31-Dec-2014] $)
  $}

  ${
    ghmsub.b $e |- B = ( Base ` S ) $.
    ghmsub.m $e |- M = ( -g ` S ) $.
    ghmsub.n $e |- N = ( -g ` T ) $.
    $( Linearity of subtraction through a group homomorphism.  (Contributed by
       Stefan O'Rear, 31-Dec-2014.) $)
    ghmsub $p |- ( ( ( F e. ( S GrpHom T ) /\ T e. W ) /\
        ( U e. B /\ V e. B ) ) -> ( F ` ( U M V ) ) =
          ( ( F ` U ) N ( F ` V ) ) ) $=
      ( co wcel wa cminusg cfv cplusg wceq eqid cghm simpl simprl cgrp ad2antrr
      ghmgrp1 simprr grpinvcl syl2anc ghmlin simpll simplr ghminv syl3anc eqtrd
      syl12anc oveq2d grpsubval fveq2d adantl cbs wf ghmf ffvelrn anim12dan syl
      sylan 3eqtr4d ) EBCUAMNZCINZOZDANZHANZOZOZDHBPQZQZBRQZMZEQZDEQZHEQZCPQZQZ
      CRQZMZDHFMZEQZWAWBGMZVOVTWAVQEQZWEMZWFVOVKVLVQANZVTWKSVKVNUBVKVLVMUCVOBUD
      NZVMWLVIWMVJVNBCEUFUEVKVLVMUGZABVPHJVPTZUHUIVRWEBCDEVQIAJVRTZWETZUJUPVOWJ
      WDWAWEVOVIVJVMWJWDSVIVJVNUKVIVJVNULWNABCEVPWCIHJWOWCTZUMUNUQUOVNWHVTSVKVN
      WGVSEAVRBVPFDHJWPWOKURUSUTVOWACVAQZNZWBWSNZOZWIWFSVKAWSEVBZVNXBBCEIAWSJWS
      TZVCXCVLWTVMXAAWSDEVDAWSHEVDVEVGWSWECWCGWAWBXDWQWRLURVFVH $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d x y z F $.  $d x y z S $.  $d x y z T $.  $d x y z U $.  $d x y z Y $.
    ghmnsgima.1 $e |- Y = ( Base ` T ) $.
    $( The image of a normal subgroup under a surjective homomorphism is
       normal.  (Contributed by Mario Carneiro, 4-Feb-2015.) $)
    ghmnsgima $p |- ( ( F e. ( S GrpHom T ) /\ U e. ( NrmSGrp ` S ) /\
      ran F = Y ) -> ( F " U ) e. ( NrmSGrp ` T ) ) $=
      ( vx vy vz co wcel cfv wceq wral cvv c0 wi eqid syl adantr cghm cnsg cima
      crn w3a csubg cv cplusg csg simp1 nsgsubg 3ad2ant2 cbs fvprc syl5eq simp3
      wn eqeq1d syl5ibr cdm dm0rn0 wne ovprc2 eleq2d cgrp ghmgrp1 wa ghmf mpdan
      wf fdm grpbn0 eqnetrd syl6bi com12 necon4bd syl5bir 3ad2ant1 syld pm2.18d
      ghmima syl3anc simprl idi wss subgss simprr sseldd ghmsub syl22anc ghmlin
      grpcl oveq1d simpl2 nsgconj wfun syl2anc ffun sseqtr4d funfvima2 eqeltrrd
      eqtrd mpd ralrimivva wfn wb oveq1 id oveq12d eleq1d ralbidv ralrn raleqdv
      ffn oveq2 ralima 3bitr3d mpbird isnsg3 sylanbrc ) DABUAJZKZCAUBLKZDUDZEMZ
      UEZDCUCZBUFLKZGUGZHUGZBUHLZJZYIBUILZJZYGKZHYGNZGENZYGBUBLKYFYBCAUFLKZBOKZ
      YHYBYCYEUJZYCYBYRYECAUKULZYFYSYFYSUQZYDPMZYSUUBUUCYFEPMUUBEBUMLPFBUMUNUOY
      FYDEPYBYCYEUPZURUSYBYCUUCYSQYEUUCDUTZPMYBYSDVAYBYSUUEPUUBYBUUEPVBZUUBYBDA
      AUAJZKZUUFUUBYAUUGDABUAVCVDUUHUUEAUMLZPUUHAVEKZUUEUUIMZAADVFZUUHUUJVGUUIU
      UIDVJUUKAADVEUUIUUIUUIRZUUMVHUUIUUIDVKSVIUUHUUJUUIPVBUULUUIAUUMVLSVMVNVOV
      PVQVRVSVTZABCDOWAWBYFYQIUGZDLZYIDLZYKJZUUPYMJZYGKZGCNZIUUINZYFUUTIGUUICYF
      UUOUUIKZYICKZVGZVGZUUOYIAUHLZJZUUOAUILZJZDLZUUSYGUVFUVKUVHDLZUUPYMJZUUSUV
      FYBYSUVHUUIKZUVCUVKUVMMYFYBUVEYTTZYFYSUVEUUNTZUVFUUJUVCYIUUIKZUVNUVFYBUUJ
      UVOABDVFSUVFUVCQYFUVCUVDWCZWDZUVFUVQQUVFCUUIYIYFCUUIWEZUVEYFYRUVTUUAUUICA
      UUMWFSZTUVFUVDQYFUVCUVDWGZWDWHZWDUUIUVGAUUOYIUUMUVGRZWLWBUVRUUIABUVHDUVIY
      MUUOOUUMUVIRZYMRZWIWJUVFUVLUURUUPYMUVFYBYSUVCUVQUVLUURMUVFYBQUVOWDUVPUVSU
      WCUVGYKABUUODYIOUUIUUMUWDYKRZWKWJWMXBUVFUVJCKZUVKYGKZUVFYCUVCUVDUWHYBYCYE
      UVEWNUVRUWBUUOYIUVIUVGCAUUIUUMUWDUWEWOWBYFUWHUWIQZUVEYFDWPZCUUEWEUWJYFUUI
      EDVJZUWKYFYBYSUWLYTUUNABDOUUIEUUMFVHWQZUUIEDWRSYFCUUIUUEUWAYFUWLUUKUWMUUI
      EDVKSWSCUVJDWTWQTXCXAXDYFYPGYDNZUUPYJYKJZUUPYMJZYGKZHYGNZIUUINZYQUVBYFDUU
      IXEZUWNUWSXFYFUWLUWTUWMUUIEDXNSZYPUWRGIUUIDYIUUPMZYOUWQHYGUXBYNUWPYGUXBYL
      UWOYIUUPYMYIUUPYJYKXGUXBXHXIXJXKXLSYFYPGYDEUUDXMYFUWRUVAIUUIYFUWTUVTUWRUV
      AXFUXAUWAUWQUUTHGUUICDYJUUQMZUWPUUSYGUXCUWOUURUUPYMYJUUQUUPYKXOWMXJXPWQXK
      XQXRGHYMYKYGBEFUWGUWFXSXT $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d x y F $.  $d x y S $.  $d x y T $.  $d x y V $.
    $( The inverse image of a normal subgroup under a homomorphism is normal.
       (Contributed by Mario Carneiro, 4-Feb-2015.) $)
    ghmnsgpreima $p |- ( ( F e. ( S GrpHom T ) /\ V e. ( NrmSGrp ` T ) ) ->
        ( `' F " V ) e. ( NrmSGrp ` S ) ) $=
      ( vx vy co wcel cnsg cfv wa csubg cv cplusg csg wral cbs eqid syl syl3anc
      cghm ccnv cima nsgsubg ghmpreima sylan2 ghmgrp1 ad2antrr simprl simprr wb
      cgrp wfn wf cdm simpll elfvdm ad2antlr ghmf syl2anc elpreima mpbid simpld
      ffn grpcl grpsubcl wceq ghmsub syl22anc ghmlin oveq1d eqtrd simplr simprd
      ffvelrn nsgconj eqeltrd mpbir2and ralrimivva isnsg3 sylanbrc ) CABUAGHZDB
      IJHZKZCUBDUCZALJHZEMZFMZANJZGZWGAOJZGZWEHZFWEPEAQJZPWEAIJHWCWBDBLJHWFDBUD
      ABCDUEUFWDWMEFWNWEWDWGWNHZWHWEHZKZKZWMWLWNHZWLCJZDHZWRAULHZWJWNHZWOWSWBXB
      WCWQABCUGUHZWRXBWOWHWNHZXCXDWDWOWPUIZWRXEWHCJZDHZWRWPXEXHKZWDWOWPUJWRCWNU
      MZWPXIUKWRWNBQJZCUNZXJWRWBBIUOZHZXLWBWCWQUPZWCXNWBWQDBIUQURZABCXMWNXKWNRZ
      XKRZUSUTZWNXKCVDSZWNWHDCVASVBZVCZWNWIAWGWHXQWIRZVETZXFWNAWKWJWGXQWKRZVFTW
      RWTWGCJZXGBNJZGZYFBOJZGZDWRWTWJCJZYFYIGZYJWRWBXNXCWOWTYLVGXOXPYDXFWNABWJC
      WKYIWGXMXQYEYIRZVHVIWRYKYHYFYIWRWBXNWOXEYKYHVGXOXPXFYBWIYGABWGCWHXMWNXQYC
      YGRZVJVIVKVLWRWCYFXKHZXHYJDHWBWCWQVMWRXLWOYOXSXFWNXKWGCVOUTWRXEXHYAVNYFXG
      YIYGDBXKXRYNYMVPTVQWRXJWMWSXAKUKXTWNWLDCVASVRVSEFWKWIWEAWNXQYCYEVTWA $.
      $( [31-Dec-2014] $)
  $}

  ${
    ghmker.1 $e |- Z = ( 0g ` T ) $.
    $( The kernel of a homomorphism is a normal subgroup.  (Contributed by
       Mario Carneiro, 4-Feb-2015.) $)
    ghmker $p |- ( ( F e. ( S GrpHom T ) /\ T e. V ) ->
        ( `' F " { Z } ) e. ( NrmSGrp ` S ) ) $=
      ( cghm co wcel csn cnsg cfv ccnv cima cgrp ghmgrp2 0nsg syl ghmnsgpreima
      wa syldan ) CABGHIZBDIZEJZBKLIZCMUDNAKLIUBUCTBOIUEABCDPBEFQRABCUDSUA $.
      $( [4-Feb-2015] $)
  $}

  ${
    ghmeqker.b $e |- B = ( Base ` S ) $.
    ghmeqker.z $e |- Z = ( 0g ` T ) $.
    ghmeqker.k $e |- K = ( `' F " { Z } ) $.
    ghmeqker.m $e |- M = ( -g ` S ) $.
    $( Two source points map to the same destination point under a group
       homomorphism iff their difference belongs to the kernel.  (Contributed
       by Stefan O'Rear, 31-Dec-2014.) $)
    ghmeqker $p |- ( ( ( F e. ( S GrpHom T ) /\ T e. W ) /\
        ( U e. B /\ V e. B ) ) -> ( ( F ` U ) = ( F ` V ) <->
          ( U M V ) e. K ) ) $=
      ( co wcel wa cfv wceq eqid cghm c0g csg ccnv csn cima sneqi imaeq2i eqtri
      eleq2i wfn cvv wb cbs wf ghmf ffn syl adantr fvex fniniseg sylancl syl5bb
      ghmgrp1 grpsubcl 3expb sylan biantrurd ghmsub eqeq1d bitr3d ghmgrp2 simpl
      cgrp ffvelrn syl2an simpr grpsubeq0 syl3anc 3bitrrd ) EBCUAOPZCIPZQZDAPZH
      APZQZQZDHGOZFPZWHAPZWHERZCUBRZSZQZDERZHERZCUCRZOZWLSZWOWPSZWIWHEUDZWLUEZU
      FZPZWGWNFXCWHFXAJUEZUFXCMXEXBXAJWLLUGUHUIUJWGEAUKZWLULPXDWNUMWCXFWFWCACUN
      RZEUOZXFBCEIAXGKXGTZUPZAXGEUQURUSCUBUTAWLWHEULVAVBVCWGWMWNWSWGWJWMWCBVNPZ
      WFWJWAXKWBBCEVDUSXKWDWEWJABGDHKNVEVFVGVHWGWKWRWLABCDEGWQHIKNWQTZVIVJVKWGC
      VNPZWOXGPZWPXGPZWSWTUMWCXMWFBCEIVLUSWCXHWDXNWFXJWDWEVMAXGDEVOVPWCXHWEXOWF
      XJWDWEVQAXGHEVOVPXGCWQWLWOWPXIWLTXLVRVSVT $.
      $( [31-Dec-2014] $)
  $}

  ${
    $d x y z F $.  $d x y z S $.  $d x y z T $.  $d x y z U $.  $d x y z W $.
    $d x y z X $.  $d x Y $.  $d x y z Z $.
    ghmf1.x $e |- X = ( Base ` S ) $.
    ghmf1.y $e |- Y = ( Base ` T ) $.
    ghmf1.z $e |- Z = ( 0g ` S ) $.
    ghmf1.u $e |- U = ( 0g ` T ) $.
    $( Two ways of saying a group homomorphism is 1-1 into its codomain.
       (Contributed by Paul Chapman, 3-Mar-2008.)  (Revised by Mario Carneiro,
       13-Jan-2015.) $)
    ghmf1 $p |- ( ( F e. ( S GrpHom T ) /\ T e. W ) -> ( F : X -1-1-> Y <->
          A. x e. X ( ( F ` x ) = U -> x = Z ) ) ) $=
      ( vy vz co wcel wa cfv wceq cghm wf1 cv wral ghmid ad2antrr eqeq2d simplr
      wi wb simpr cgrp simplll ghmgrp1 grpidcl f1fveq syl12anc bitr3d ralrimiva
      syl biimpd wf ghmf adantr eqid ghmsub adantlr eqeq1d grpsubcl 3expb sylan
      csg fveq2 eqeq1 imbi12d rcla4v sylc sylbird ghmgrp2 simprl ffvelrn simprr
      syl2anc grpsubeq0 syl3anc 3imtr3d ralrimivva dff13 sylanbrc impbida ) EBC
      UAPQZCFQZRZGHEUBZAUCZESZDTZWOITZUIZAGUDZWMWNRZWSAGXAWOGQZRZWQWRXCWPIESZTZ
      WQWRXCXDDWPWMXDDTWNXBBCEFIDLMUEUFUGXCWNXBIGQZXEWRUJWMWNXBUHXAXBUKXCBULQZX
      FXCWKXGWKWLWNXBUMBCEUNZUTGBIJLUOUTGHWOIEUPUQURVAUSWMWTRZGHEVBZNUCZESZOUCZ
      ESZTZXKXMTZUIZOGUDNGUDWNWMXJWTBCEFGHJKVCVDZXIXQNOGGXIXKGQZXMGQZRZRZXLXNCV
      LSZPZDTZXKXMBVLSZPZITZXOXPYBYEYGESZDTZYHYBYIYDDWMYAYIYDTWTGBCXKEYFYCXMFJY
      FVEZYCVEZVFVGVHYBYGGQZWTYJYHUIZXIXGYAYMWKXGWLWTXHUFZXGXSXTYMGBYFXKXMJYKVI
      VJVKWMWTYAUHWSYNAYGGWOYGTZWQYJWRYHYPWPYIDWOYGEVMVHWOYGIVNVOVPVQVRYBCULQZX
      LHQZXNHQZYEXOUJWMYQWTYABCEFVSUFYBXJXSYRXIXJYAXRVDZXIXSXTVTZGHXKEWAWCYBXJX
      TYSYTXIXSXTWBZGHXMEWAWCHCYCDXLXNKMYLWDWEYBXGXSXTYHXPUJXIXGYAYOVDUUAUUBGBY
      FIXKXMJLYKWDWEWFWGNOGHEWHWIWJ $.
      $( [13-Jan-2015] $) $( [31-Dec-2014] $)
  $}

  ${
    $d x y F $.  $d x y S $.  $d x y T $.  $d x y W $.  $d x y X $.
    $d x y Y $.
    ghmf1o.x $e |- X = ( Base ` S ) $.
    ghmf1o.y $e |- Y = ( Base ` T ) $.
    ghmf1o.z $e |- Z = ( 0g ` S ) $.
    ghmf1o.u $e |- U = ( 0g ` T ) $.
    $( A bijective group homomorphism is an isomorphism.  (Contributed by Mario
       Carneiro, 13-Jan-2015.) $)
    ghmf1o $p |- ( ( F e. ( S GrpHom T ) /\ T e. W ) ->
      ( F : X -1-1-onto-> Y <-> `' F e. ( T GrpHom S ) ) ) $=
      ( vx co wcel wa cfv wceq syl syl2anc vy cghm wf1o ccnv cgrp wf cv ghmgrp2
      cplusg wral ghmgrp1 adantr jca f1ocnv adantl simpll simprl ffvelrn simprr
      f1of eqid ghmlin syl12anc simplr f1ocnvfv2 oveq12d eqtrd wi grpcl syl3anc
      f1ocnvfv mpd ralrimivva wb ad2antrr mpbir2and wfn ghmf ffn syl2anr dff1o4
      isghm id sylanbrc impbida ) DABUBNOZBEOZPZFGDUCZDUDZBAUBNOZWHWIPZWKBUEOZA
      UEOZPZGFWJUFZMUGZUAUGZBUIQZNZWJQWQWJQZWRWJQZAUIQZNZRZUAGUJMGUJZPZWHWOWIWH
      WMWNABDEUHWFWNWGABDUKZULZUMULWLWPXFWLGFWJUCZWPWIXJWHFGDUNUOGFWJUTSZWLXEMU
      AGGWLWQGOZWRGOZPZPZXDDQZWTRZXEXOXPXADQZXBDQZWSNZWTXOWHXAFOZXBFOZXPXTRWHWI
      XNUPZXOWPXLYAWLWPXNXKULZWLXLXMUQZGFWQWJURTZXOWPXMYBYDWLXLXMUSZGFWRWJURTZX
      CWSABXADXBEFIXCVAZWSVAZVBVCXOXRWQXSWRWSXOWIXLXRWQRWHWIXNVDZYEFGWQDVETXOWI
      XMXSWRRYKYGFGWRDVETVFVGXOWIXDFOZXQXEVHYKXOWNYAYBYLXOWHWNYCXISYFYHFXCAXAXB
      IYIVIVJFGXDWTDVKTVLVMUMWLWNWKWOXGPVNWFWNWGWIXHVOUAMWSXCBAWJUEGFJIYJYIWBSV
      PWHWKPZDFVQZWJGVQZWIYMFGDUFZYNWHYPWKABDEFGIJVRULFGDVSSYMWPYOWKWKWNWPWHWKW
      CXIBAWJUEGFJIVRVTGFWJVSSFGDWAWDWE $.
      $( [13-Jan-2015] $)
  $}

  ${
    $d w x y z A $.  $d w x y z G $.  $d x y M $.  $d w x N $.  $d w x y z P $.
    $d w y z F $.  $d w x y z S $.  $d w x y z X $.
    conjghm.x $e |- X = ( Base ` G ) $.
    conjghm.p $e |- P = ( +g ` G ) $.
    conjghm.m $e |- M = ( -g ` G ) $.
    ${
      conjghm.f $e |- F = ( x e. X |-> ( ( A P x ) M A ) ) $.
      $( Conjugation is an automorphism of the group.  (Contributed by Mario
         Carneiro, 13-Jan-2015.) $)
      conjghm $p |- ( ( G e. Grp /\ A e. X ) ->
        ( F e. ( G GrpHom G ) /\ F : X -1-1-onto-> X ) ) $=
        ( vy wcel wa co cfv wceq syl3anc syl13anc oveq1d vz cgrp cghm wf1o wral
        wf cv simpl jca adantr simplr simpr grpcl grpsubcl simprl simprr grpass
        fmptd grpnpcan grpaddsubass 3eqtr2rd oveq2d 3eqtr4d oveq2 ovex ad2antrl
        fvmpt ad2antll oveq12d ralrimivva isghm mpbir2and cminusg eqid grpinvcl
        syl wb syl2anc syldan adantrr grplcan c0g grplid eqtrd eqtr3d grpsubadd
        grplinv eqeq2d 3bitr4d eqcom 3bitr4g f1o2d ) EUBMZBGMZNZDEEUCOMZGGDUDWO
        WPWMWMNZGGDUFZLUGZUAUGZCOZDPZWSDPZWTDPZCOZQZUAGUELGUEZNZWOWMWMWMWNUHZXI
        UIWOWRXGWOAGBAUGZCOZBFOZGDWOXJGMZNZWMXKGMZWNXLGMWOWMXMXIUJZXNWMWNXMXOXP
        WMWNXMUKZWOXMULZGCEBXJHIUMRZXQGEFXKBHJUNRZKURWOXFLUAGGWOWSGMZWTGMZNZNZB
        XACOZBFOZBWSCOZBFOZBWTCOZBFOZCOZXBXEYDYHBCOZWTBFOZCOZYHBYMCOZCOZYFYKYDW
        MYHGMZWNYMGMZYNYPQWOWMYCXIUJZYDWMYGGMZWNYQYSYDWMWNYAYTYSWMWNYCUKZWOYAYB
        UOZGCEBWSHIUMRZUUAGEFYGBHJUNRUUAYDWMYBWNYRYSWOYAYBUPZUUAGEFWTBHJUNRGCEY
        HBYMHIUQSYDYNYGYMCOZYGWTCOZBFOZYFYDYLYGYMCYDWMYTWNYLYGQYSUUCUUAGCEFYGBH
        IJUSRTYDWMYTYBWNUUGUUEQYSUUCUUDUUAGCEFYGWTBHIJUTSYDUUFYEBFYDWMWNYAYBUUF
        YEQYSUUAUUBUUDGCEBWSWTHIUQSTVAYDYJYOYHCYDWMWNYBWNYJYOQYSUUAUUDUUAGCEFBW
        TBHIJUTSVBVCYDXAGMZXBYFQYDWMYAYBUUHYSUUBUUDGCEWSWTHIUMRAXAXLYFGDXJXAQXK
        YEBFXJXABCVDTKYEBFVEVGVPYDXCYHXDYJCYAXCYHQWOYBAWSXLYHGDXJWSQXKYGBFXJWSB
        CVDTKYGBFVEVGVFYBXDYJQWOYAAWTXLYJGDXJWTQXKYIBFXJWTBCVDTKYIBFVEVGVHVIVCV
        JUIWOWMWPWQXHNVQXIUALCCEEDUBGGHHIIVKVPVLWOALGGXLBEVMPZPZWSBCOZCOZDKXTWO
        YANZWMUUJGMZUUKGMZUULGMWOWMYAXIUJZUUMWMWNUUNUUPWMWNYAUKZGEUUIBHUUIVNZVO
        ZVRUUMWMYAWNUUOUUPWOYAULUUQGCEWSBHIUMRZGCEUUJUUKHIUMRWOXMYANZNZUULXJQZX
        LWSQZXJUULQWSXLQUVBUULUUJXKCOZQZUUKXKQZUVCUVDUVBWMUUOXOUUNUVFUVGVQWOWMU
        VAXIUJZWOUVAYAUUOWOXMYAUPZUUTVSWOXMXOYAXSVTZWOUUNUVAUUSUJZGCEUUKXKUUJHI
        WASUVBXJUVEUULUVBUUJBCOZXJCOZXJUVEUVBUVMEWBPZXJCOZXJUVBUVLUVNXJCUVBWMWN
        UVLUVNQUVHWOXMWNYAXQVTZGCEUUIUVNBHIUVNVNZUURWGVRTUVBWMXMUVOXJQUVHWOXMXM
        YAXRVTZGCEUVNXJHIUVQWCVRWDUVBWMUUNWNXMUVMUVEQUVHUVKUVPUVRGCEUUJBXJHIUQS
        WEWHUVBWMXOWNYAUVDUVGVQUVHUVJUVPUVIGCEFXKBWSHIJWFSWIXJUULWJWSXLWJWKWLUI
        $.
        $( [13-Jan-2015] $)
    $}

    conjsubg.f $e |- F = ( x e. S |-> ( ( A P x ) M A ) ) $.
    $( A conjugated subgroup is also a subgroup.  (Contributed by Mario
       Carneiro, 13-Jan-2015.) $)
    conjsubg $p |- ( ( S e. ( SubGrp ` G ) /\ A e. X ) ->
      ran F e. ( SubGrp ` G ) ) $=
      ( csubg wcel wa co cmpt crn syl cgrp cfv cima wss wceq subgss adantr cres
      cv df-ima resmpt syl6eqr rneqd syl5eq cghm wf1o subgrcl eqid sylan simpld
      conjghm simpl ghmima syl3anc eqeltrrd ) DFMUAZNZBHNZOZAHBAUHCPBGPZQZDUBZE
      RZVEVHDHUCZVKVLUDVFVMVGHDFIUEUFVMVKVJDUGZRVLVJDUIVMVNEVMVNADVIQEAHDVIUJLU
      KULUMSVHVJFFUNPNZVFFTNZVKVENVHVOHHVJUOZVFVPVGVOVQODFUPZABCVJFGHIJKVJUQUTU
      RUSVFVGVAZVHVFVPVSVRSFFDVJTVBVCVD $.
      $( [13-Jan-2015] $)

    $( A conjugated subgroup is equinumerous to the original subgroup.
       (Contributed by Mario Carneiro, 18-Jan-2015.) $)
    conjsubgen $p |- ( ( S e. ( SubGrp ` G ) /\ A e. X ) ->
      S ~~ ran F ) $=
      ( wcel wf1o wa wf1 co cmpt syl wceq cfv crn cen wbr cv cres wss cghm cgrp
      csubg subgrcl conjghm sylan simprd f1of1 subgss adantr f1ssres syl2anc wb
      eqid resmpt syl6eqr f1eq1 mpbid f1f1orn f1oeng syldan ) DFUJUAZMZBHMZDEUB
      ZENZDVLUCUDVJVKOZDHEPZVMVNDHAHBAUECQBGQZRZDUFZPZVOVNHHVQPZDHUGZVSVNHHVQNZ
      VTVNVQFFUHQMZWBVJFUIMVKWCWBODFUKABCVQFGHIJKVQVAULUMUNHHVQUOSVJWAVKHDFIUPU
      QZHHDVQURUSVNVRETVSVOUTVNVRADVPRZEVNWAVRWETWDAHDVPVBSLVCDHVREVDSVEDHEVFSD
      VLVIEVGVH $.
      $( [18-Jan-2015] $)

    ${
      conjnmz.1 $e |- N = { y e. X |
        A. z e. X ( ( y P z ) e. S <-> ( z P y ) e. S ) } $.
      $( A subgroup is unchanged under conjugation by an element of its
         normalizer. $)
      conjnmz $p |- ( ( S e. ( SubGrp ` G ) /\ A e. N ) -> S = ran F ) $=
        ( wcel co wceq syl2anc vw csubg cfv wa cv cminusg cgrp subgrcl ad2antrr
        crn wb wral crab ssrab2 eqsstri simplr sseldi eqid subgss adantr sselda
        grpinvcl wss grpass syl13anc grprinv oveq1d 3eqtr3d simpr eqeltrd grpcl
        c0g grplid syl3anc nmzbi mpbid eqeltrrd oveq2 fvmpt syl grppncan 3eqtrd
        ovex wfn cvv fnmpt mprg fnfvelrn sylancr ex ssrdv grpaddsubass grpnpcan
        a1i wf grpsubcl mpbird fmptd frn eqssd ) FHUBUCQZDJQZUDZFGUJZXCUAFXDXCU
        AUEZFQZXEXDQXCXFUDZDHUFUCZUCZXEDERZERZGUCZXEXDXGXLDXKERZDIRZXJDIRZXEXGX
        KFQZXLXNSXGXIXEERZDERZXKFXGHUGQZXIKQZXEKQZDKQZXRXKSXAXSXBXFFHUHZUIZXGXS
        YBXTYDXGJKDJBUEZCUEZERFQYFYEERFQUKCKULZBKUMKPYGBKUNUOZXAXBXFUPZUQZKHXHD
        LXHURZVBTZXCFKXEXAFKVCXBKFHLUSUTZVAZYJKEHXIXEDLMVDVEXGDXQERZFQZXRFQZXGY
        OXEFXGDXIERZXEERZHVLUCZXEERZYOXEXGYRYTXEEXGXSYBYRYTSYDYJKEHXHYTDLMYTURZ
        YKVFTZVGXGXSYBXTYAYSYOSYDYJYLYNKEHDXIXELMVDVEXGXSYAUUAXESYDYNKEHYTXELMU
        UBVMTVHXCXFVIVJXGXBXQKQZYPYQUKYIXGXSXTYAUUDYDYLYNKEHXIXELMVKVNBCDXQEFJK
        PVOTVPVQZAXKDAUEZERZDIRZXNFGUUFXKSUUGXMDIUUFXKDEVRVGOXMDIWCVSVTXGXMXJDI
        XGYRXJERZYTXJERZXMXJXGYRYTXJEUUCVGXGXSYBXTXJKQZUUIXMSYDYJYLXGXSYAYBUUKY
        DYNYJKEHXEDLMVKVNZKEHDXIXJLMVDVEXGXSUUKUUJXJSYDUULKEHYTXJLMUUBVMTVHVGXG
        XSYAYBXOXESYDYNYJKEHIXEDLMNWAVNWBXGGFWDZXPXLXDQUUHWEQZUUMAFAFUUHGWEOWFU
        UNUUFFQZUUGDIWCWNWGUUEFXKGWHWIVQWJWKXCFFGWOXDFVCXCAFUUHFGXCUUOUDZUUHDUU
        FDIRZERZFUUPXSYBUUFKQZYBUUHUURSXAXSXBUUOYCUIZUUPJKDYHXAXBUUOUPZUQZXCFKU
        UFYMVAZUVBKEHIDUUFDLMNWLVEUUPUURFQZUUQDERZFQZUUPUVEUUFFUUPXSUUSYBUVEUUF
        SUUTUVCUVBKEHIUUFDLMNWMVNXCUUOVIVJUUPXBUUQKQZUVDUVFUKUVAUUPXSUUSYBUVGUU
        TUVCUVBKHIUUFDLNWPVNBCDUUQEFJKPVOTWQVJOWRFFGWSVTWT $.
        $( [18-Jan-2015] $)

      $( Alternative condition for elementhood in the normalizer. $)
      conjnmzb $p |- ( S e. ( SubGrp ` G ) ->
        ( A e. N <-> ( A e. X /\ S = ran F ) ) ) $=
        ( wcel wceq wa co vw csubg cfv crn cv wb wral crab ssrab2 eqsstri simpr
        sseldi conjnmz jca simprl simplrr eleq2d wrex subgrcl ad3antrrr simpllr
        wss subgss ad2antrr sselda grpaddsubass syl13anc eqeq1d grpsubcl simplr
        cgrp syl3anc grplcan grpsubadd 3bitrd eqcom 3bitr4g rexbidva ovex eqeq1
        adantlrr rexbidv rnmpt elab2 risset bitrd ralrimiva sylanbrc impbida
        elnmz ) FHUBUCQZDJQZDKQZFGUDZRZSZWKWLSZWMWOWQJKDJBUEZCUEZETFQWSWRETFQUF
        CKUGZBKUHKPWTBKUIUJWKWLUKULABCDEFGHIJKLMNOPUMUNWKWPSZWMDUAUEZETZFQZXBDE
        TZFQZUFZUAKUGWLWKWMWOUOXAXGUAKXAXBKQZSZXDXCWNQZXFXIFWNXCWKWMWOXHUPUQXIX
        CDAUEZETDITZRZAFURZXKXERZAFURZXJXFWKWMXHXNXPUFWOWKWMSZXHSZXMXOAFXRXKFQZ
        SZXLXCRZXEXKRZXMXOXTYADXKDITZETZXCRZYCXBRZYBXTXLYDXCXTHVKQZWMXKKQZWMXLY
        DRWKYGWMXHXSFHUSUTZWKWMXHXSVAZXRFKXKWKFKVBWMXHKFHLVCVDVEZYJKEHIDXKDLMNV
        FVGVHXTYGYCKQZXHWMYEYFUFYIXTYGYHWMYLYIYKYJKHIXKDLNVIVLXQXHXSVJZYJKEHYCX
        BDLMVMVGXTYGYHWMXHYFYBUFYIYKYJYMKEHIXKDXBLMNVNVGVOXCXLVPXKXEVPVQVRWAWRX
        LRZAFURXNBXCWNDXBEVSWRXCRYNXMAFWRXCXLVTWBABFXLGOWCWDAXEFWEVQWFWGBCUADEF
        JKPWJWHWI $.
        $( [18-Jan-2015] $)
    $}

    $( A normal subgroup is unchanged under conjugation. $)
    conjnsg $p |- ( ( S e. ( NrmSGrp ` G ) /\ A e. X ) -> S = ran F ) $=
      ( vy vz cnsg cfv wcel cv co wceq wb wral crab csubg isnsg4 simprbi eleq2d
      crn eqid biimpar nsgsubg conjnmz sylan syldan ) DFOPQZBHQZBMRZNRZCSDQURUQ
      CSDQUANHUBMHUCZQZDEUHTZUOUTUPUOUSHBUODFUDPQZUSHTMNCDFUSHUSUIZIJUEUFUGUJUO
      VBUTVADFUKAMNBCDEFGUSHIJKLVCULUMUN $.
      $( [18-Jan-2015] $)
  $}

