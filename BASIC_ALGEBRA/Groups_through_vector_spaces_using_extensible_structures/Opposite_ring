$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Opposite ring
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c oppR $. $( Opposite ring $)

  $( The opposite ring operation. $)
  coppr $a class oppR $.

  ${
    $d v w x y z $.
    $( Define an opposite ring, which is the same as the original ring but with
       multiplication written the other way around. $)
    df-oppr $a |- oppR = ( w e. _V |-> ( w sSet
      <. ( .r ` ndx ) , ( x e. ( Base ` w ) , y e. ( Base ` w ) |->
        ( y ( .r ` w ) x ) ) >. ) ) $.
  $}

  ${
    $d w x y R $.  $d w x y B $.  $d w x y T $.  $d x y X $.  $d x y Y $.
    opprval.1 $e |- B = ( Base ` R ) $.
    opprval.2 $e |- T = ( .r ` R ) $.
    opprval.3 $e |- O = ( oppR ` R ) $.
    $( Value of the opposite ring. $)
    opprval $p |- ( R e. V -> O = ( R sSet
      <. ( .r ` ndx ) , ( x e. B , y e. B |-> ( y T x ) ) >. ) ) $=
      ( vw wcel cvv cmulr cfv cv co cmpt2 csts cbs cnx wceq elex coppr id fveq2
      cop syl6eqr oveqd mpt2eq123dv opeq2d oveq12d df-oppr ovex fvmpt syl5eq
      syl ) DGLDMLZFDUANOZABCCBPZAPZEQZRZUGZSQZUBDGUCURFDUDOVEJKDKPZUSABVFTOZVG
      UTVAVFNOZQZRZUGZSQVEMUDVFDUBZVFDVKVDSVLUEVLVJVCUSVLABVGVGVICCVBVLVGDTOCVF
      DTUFHUHZVMVLVHEUTVAVLVHDNOEVFDNUFIUHUIUJUKULABKUMDVDSUNUOUPUQ $.
      $( [1-Dec-2014] $)

    opprmulfval.4 $e |- K = ( .r ` O ) $.
    $( Value of the multiplication operation of an opposite ring. $)
    opprmulfval $p |- ( R e. V -> K = ( x e. B , y e. B |-> ( y T x ) ) ) $=
      ( vw wcel cmulr cfv cv co cvv cbs cmpt2 cnx csts opprval fveq2d wceq fvex
      cop eqeltri mpt2ex mulrid setsid mpan2 eqtr4d syl5eq ) DHNZFGOPZABCCBQAQE
      RZUAZLUPUQDUBOPUSUHUCRZOPZUSUPGUTOABCDEGHIJKUDUEUPUSSNUSVAUFABCCURCDTPSID
      TUGUIZVBUJHUSOSDMMUKULUMUNUO $.
      $( [1-Dec-2014] $)

    $( Value of the multiplication operation of an opposite ring. $)
    opprmul $p |- ( ( X e. B /\ Y e. B ) -> ( Y T X ) = ( X K Y ) ) $=
      ( vx vy wcel wa co cv cmpt2 cbs wceq cdm elfvdm eleq2s adantr opprmulfval
      cfv syl oveqd oveq12 ancoms eqid ovex ovmpt2a eqtr2d ) FANZGANZOZFGDPFGLM
      AAMQZLQZCPZRZPGFCPZUQDVAFGUQBSUAZNZDVATUOVDUPVDFBSUFAFBSUBHUCUDLMABCDEVCH
      IJKUEUGUHLMFGAAUTVBVAURGTUSFTUTVBTURGUSFCUIUJVAUKGFCULUMUN $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d s x y z R $.  $d s E $.  $d x y z O $.  $d x y V $.
    opprbas.1 $e |- O = ( oppR ` R ) $.
    ${
      opprlem.2 $e |- E = ( s e. _V |-> ( s ` ( E ` ndx ) ) ) $.
      opprlem.3 $e |- -. ( .r ` ndx ) e. { ( E ` ndx ) } $.
      $( Lemma for ~ opprbas and ~ oppradd . $)
      opprlem $p |- ( E ` R ) = ( E ` O ) $=
        ( vx vy cvv wcel cfv cnx cmulr cv co wn eqid fveq2d c0 wceq cbs cop csn
        cmpt2 csts wne fvex elsnc necon3bbii mpbi necomi setsnid opprval eqtr4d
        fvprc coppr syl5eq str0 syl6eqr pm2.61i ) AJKZABLZCBLZUAVBVCAMNLZHIAUBL
        ZVFIOHOANLZPUEZUCUFPZBLVDVHVEBJADFVEMBLZVEVJUDKZQVEVJUGGVKVEVJVEVJMNUHU
        IUJUKULUMVBCVIBHIVFAVGCJVFRVGREUNSUOVBQZVCTVDABUPVLVDTBLTVLCTBVLCAUQLTE
        AUQUPURSBVJDFUSUTUOVA $.
        $( [1-Dec-2014] $)
    $}

    ${
      opprbas.2 $e |- B = ( Base ` R ) $.
      $( Base set of an opposite ring. $)
      opprbas $p |- B = ( Base ` O ) $=
        ( cbs cfv baseid cnx cmulr csn wcel cplusg cpr rnglem snsspr1 sseli mto
        vx opprlem eqtri ) ABFGCFGEBFCSDSHIJGZIFGZKZLUBUCIMGZNZLOUDUFUBUCUEPQRT
        UA $.
        $( [1-Dec-2014] $)
    $}

    ${
      oppradd.2 $e |- P = ( +g ` R ) $.
      $( Addition operation of an opposite ring. $)
      oppradd $p |- P = ( +g ` O ) $=
        ( vx cplusg cfv plusgid cnx cmulr csn wcel cbs cpr rnglem snsspr2 sseli
        mto opprlem eqtri ) ABGHCGHEBGCFDFIJKHZJGHZLZMUBJNHZUCOZMPUDUFUBUEUCQRS
        TUA $.
        $( [1-Dec-2014] $)
    $}

    $( An opposite ring is a ring. $)
    opprrng $p |- ( R e. Ring -> O e. Ring ) $=
      ( vx vy vz wcel cbs cplusg cmulr wceq eqid cv co opprmul syl13anc syl3anc
      cfv syl2anc 3eqtr3d crg cur opprbas a1i oppradd eqidd cgrp rnggrp grpprop
      sylib w3a 3adant1 rngcl 3com23 eqeltrrd simpl simpr3 simpr2 simpr1 rngass
      wa 3adant3r3 3eqtr3rd oveq1d 3adant3r1 oveq2d rngdir rngacl oveq12d rngdi
      rngidcl adantr simpr rngridm eqtr3d rnglidm isrngd ) AUAGZDEFAHRZAIRZBBJR
      ZAUBRZVSBHRKVRVSABCVSLZUCZUDVTBIRKVRVTABCVTLZUEZUDVRWAUFVRAUGGBUGGAUHABWD
      WFUIUJVRDMZVSGZEMZVSGZUKWIWGAJRZNZWGWIWANZVSWHWJWLWMKZVRVSAWKWABWGWIWCWKL
      ZCWALZOULZVRWJWHWLVSGZVSAWKWIWGWCWOUMUNZUOVRWHWJFMZVSGZUKZVAZWLWTWANZWGWT
      WIWKNZWANZWMWTWANWGWIWTWANZWANXCXEWGWKNZWTWLWKNZXFXDXCVRXAWJWHXHXIKVRXBUP
      ZVRWHWJXAUQZVRWHWJXAURZVRWHWJXAUSZVSAWKWTWIWGWCWOUTPXCWHXEVSGZXHXFKXMXCVR
      XAWJXNXJXKXLVSAWKWTWIWCWOUMQVSAWKWABWGXEWCWOCWPOSXCWRXAXIXDKVRWHWJWRXAWSV
      BXKVSAWKWABWLWTWCWOCWPOSVCXCWLWMWTWAVRWHWJWNXAWQVBZVDXCXEXGWGWAVRWJXAXEXG
      KZWHWJXAXPVRVSAWKWABWIWTWCWOCWPOULVEZVFTXCWIWTVTNZWGWKNZWLWTWGWKNZVTNZWGX
      RWANZWMWGWTWANZVTNXCVRWJXAWHXSYAKXJXLXKXMVSVTAWKWIWTWGWCWEWOVGPXCWHXRVSGZ
      XSYBKXMXCVRWJXAYDXJXLXKVSVTAWIWTWCWEVHQVSAWKWABWGXRWCWOCWPOSXCWLWMXTYCVTX
      OXCWHXAXTYCKXMXKVSAWKWABWGWTWCWOCWPOSZVITXCWTWGWIVTNZWKNZXTXEVTNZYFWTWANZ
      YCXGVTNXCVRXAWHWJYGYHKXJXKXMXLVSVTAWKWTWGWIWCWEWOVJPXCYFVSGZXAYGYIKXCVRWH
      WJYJXJXMXLVSVTAWGWIWCWEVHQXKVSAWKWABYFWTWCWOCWPOSXCXTYCXEXGVTYEXQVITVSAWB
      WCWBLZVKZVRWHVAZWGWBWKNZWBWGWANZWGYMWBVSGZWHYNYOKVRYPWHYLVLZVRWHVMZVSAWKW
      ABWBWGWCWOCWPOSVSAWKWBWGWCWOYKVNVOYMWBWGWKNZWGWBWANZWGYMWHYPYSYTKYRYQVSAW
      KWABWGWBWCWOCWPOSVSAWKWBWGWCWOYKVPVOVQ $.
      $( [1-Dec-2014] $)

    $( Bidirectional form of ~ opprrng .  (Contributed by Mario Carneiro,
       6-Dec-2014.) $)
    opprrngb $p |- ( R e. Ring <-> O e. Ring ) $=
      ( vx vy crg wcel opprrng cfv eqid wtru cbs opprbas a1i cv cplusg co cmulr
      wceq wa coppr wb eqidd oppradd oveqi opprmul ancoms eqtrd adantl rngpropd
      trud sylibr impbii ) AFGZBFGZABCHUOBUAIZFGZUNBUPUPJZHUNUQUBKDEALIZAUPKUSU
      CUSUPLISKUSBUPURUSABCUSJZMZMNDOZEOZAPIZQVBVCUPPIZQSKVBUSGZVCUSGZTZTVDVEVB
      VCVDBUPURVDABCVDJUDUDUENVHVBVCARIZQZVBVCUPRIZQZSKVHVJVCVBBRIZQZVLVGVFVJVN
      SUSAVIVMBVCVBUTVIJCVMJZUFUGUSBVMVKUPVBVCVAVOURVKJUFUHUIUJUKULUM $.
      $( [6-Dec-2014] $)

    ${
      oppr0.2 $e |- Z = ( 0g ` R ) $.
      $( Additive identity of an opposite ring. $)
      oppr0 $p |- ( R e. V -> Z = ( 0g ` O ) ) $=
        ( vx vy wcel cv cplusg cfv co wceq cbs wral eqid grpidval cvv coppr c0g
        crio fvex eqeltri opprbas oppradd ax-mp syl6eqr ) ACIDGJHJZAKLZMUINHAOL
        ZPGUKUBZBUALZHUKUJGADCUKQZUJQZFRBSIUMULNBATLSEATUCUDHUKUJGBUMSUKABEUNUE
        UJABEUOUFUMQRUGUH $.
        $( [1-Dec-2014] $)
    $}

    ${
      oppr1.2 $e |- U = ( 1r ` R ) $.
      $( Multiplicative identity of an opposite ring. $)
      oppr1 $p |- ( R e. V -> U = ( 1r ` O ) ) $=
        ( vx vy wcel cv cmulr cfv co wceq wa wral crio eqid rngidval cvv eqeq1d
        cbs ancom opprmul ancoms anbi12d syl5bbr ralbidva riotabiia syl6eq fvex
        cur coppr eqeltri opprbas ax-mp syl6eqr ) ADIZBGJZHJZCKLZMZUTNZUTUSVAMZ
        UTNZOZHAUBLZPZGVGQZCULLZURBUSUTAKLZMZUTNZUTUSVKMZUTNZOZHVGPZGVGQVIHGVGA
        VKBDVGRZVKRZFSVQVHGVGUSVGIZVPVFHVGVPVOVMOVTUTVGIZOZVFVOVMUCWBVOVCVMVEWB
        VNVBUTVGAVKVACUSUTVRVSEVARZUDUAWBVLVDUTWAVTVLVDNVGAVKVACUTUSVRVSEWCUDUE
        UAUFUGUHUIUJCTIVJVINCAUMLTEAUMUKUNHGVGCVAVJTVGACEVRUOWCVJRSUPUQ $.
        $( [1-Dec-2014] $)
    $}

    ${
      opprneg.2 $e |- N = ( invg ` R ) $.
      $( The negative function in an opposite ring.  (Contributed by Mario
         Carneiro, 5-Dec-2014.) $)
      opprneg $p |- N = ( invg ` O ) $=
        ( vx vy cvv wcel cminusg cfv wceq cbs c0g crio cmpt grpinvfval coppr c0
        eqid cv cplusg co oppr0 eqeq2d riotabidv mpteq2dv eqtrd eqeltri opprbas
        fvex oppradd ax-mp syl6eqr wn fvprc syl5eq fveq2d wfn wral ral0 0ex cnx
        baseid str0 fnmpt fn0 mpbi syl6eq eqtr4d pm2.61i ) AHIZBCJKZLVLBFAMKZGU
        AZFUAZAUBKZUCZCNKZLZGVNOZPZVMVLBFVNVRANKZLZGVNOZPWBFGVNVQABWCHVNTZVQTZW
        CTZEQVLFVNWEWAVLWDVTGVNVLWCVSVRACHWCDWHUDUEUFUGUHCHIVMWBLCARKZHDARUKUIF
        GVNVQCVMVSHVNACDWFUJVQACDWGULVSTVMTQUMUNVLUOZBAJKZVMEWJWKSVMAJUPWJVMSJK
        ZSWJCSJWJCWISDARUPUQURWLSUSZWLSLVOVPSUBKZUCSNKZLGSOZHIZFSUTWMWQFVAFSWPW
        LHSHIWLFSWPPLVBFGSWNSWLWOHMVCMKFFVDVEWNTWOTWLTQUMVFUMWLVGVHVIVJUQVK $.
        $( [5-Dec-2014] $)
    $}

    $( Being a subgroup is a symmetric property.  (Contributed by Mario
       Carneiro, 6-Dec-2014.) $)
    opprsubg $p |- ( SubGrp ` R ) = ( SubGrp ` O ) $=
      ( vx csubg cfv cgrp wcel cbs cress co w3a eqid cplusg grpprop cvv ressbas
      wceq ax-mp eqtr3i cv wss opprbas oppradd biid cin vex ressplusg 3anbi123i
      issubg 3bitr4i eqriv ) DAEFZBEFZAGHZDUAZAIFZUBZAUPJKZGHZLBGHZURBUPJKZGHZL
      UPUMHUPUNHUOVAURURUTVCABUQABCUQMZUCZANFZABCVFMZUDZOURUEUSVBUPUQUFZUSIFZVB
      IFZUPPHZVIVJRDUGZUPUQUSPAUSMZVDQSVLVIVKRVMUPUQVBPBVBMZVEQSTVFUSNFVBNFUPVF
      AUSVNVGUHUPVFBVBVOVHUHTOUIUQUPAVDUJUQUPBVEUJUKUL $.
      $( [6-Dec-2014] $)
  $}

