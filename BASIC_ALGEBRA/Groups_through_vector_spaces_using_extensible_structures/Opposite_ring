$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Opposite ring
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c oppR $. $( Opposite ring $)

  $( The opposite ring operation. $)
  coppr $a class oppR $.

  ${
    $d v w x y z $.
    $( Define an opposite ring, which is the same as the original ring but with
       multiplication written the other way around. $)
    df-oppr $a |- oppR = ( w e. _V |-> ( w sSet
      { <. ( .r ` ndx ) , ( x e. ( Base ` w ) , y e. ( Base ` w ) |->
        ( y ( .r ` w ) x ) ) >. } ) ) $.
  $}

  ${
    $d w x y R $.  $d w x y B $.  $d w x y T $.  $d x y X $.  $d x y Y $.
    opprval.1 $e |- B = ( Base ` R ) $.
    opprval.2 $e |- T = ( .r ` R ) $.
    opprval.3 $e |- O = ( oppR ` R ) $.
    $( Value of the opposite ring. $)
    opprval $p |- ( R e. V -> O = ( R sSet
      { <. ( .r ` ndx ) , ( x e. B , y e. B |-> ( y T x ) ) >. } ) ) $=
      ( vw wcel cvv cmulr cfv cv co cmpt2 csts cbs cnx cop csn wceq coppr fveq2
      elex id syl6eqr oveqd mpt2eq123dv opeq2d sneqd oveq12d df-oppr ovex fvmpt
      syl5eq syl ) DGLDMLZFDUANOZABCCBPZAPZEQZRZUBZUCZSQZUDDGUGUTFDUEOVHJKDKPZV
      AABVITOZVJVBVCVINOZQZRZUBZUCZSQVHMUEVIDUDZVIDVOVGSVPUHVPVNVFVPVMVEVAVPABV
      JVJVLCCVDVPVJDTOCVIDTUFHUIZVQVPVKEVBVCVPVKDNOEVIDNUFIUIUJUKULUMUNABKUODVG
      SUPUQURUS $.
      $( [1-Dec-2014] $)

    opprmulfval.4 $e |- K = ( .r ` O ) $.
    $( Value of the multiplication operation of an opposite ring. $)
    opprmulfval $p |- ( R e. V -> K = ( x e. B , y e. B |-> ( y T x ) ) ) $=
      ( vw wcel cmulr cfv cv co cnx cvv cmpt2 cop csts opprval fveq2d wceq fvex
      csn eqeltri mpt2ex snex funsn mulrid opex snid setsid mpan2 eqtr4d syl5eq
      cbs ) DHNZFGOPZABCCBQAQERZUAZLVAVBDSOPZVDUBZUHZUCRZOPZVDVAGVHOABCDEGHIJKU
      DUEVAVDTNVDVIUFABCCVCCDUTPTIDUTUGUIZVJUJHVDVGOTDMVFUKVEVDSOUGULMUMVFVEVDU
      NUOUPUQURUS $.
      $( [1-Dec-2014] $)

    $( Value of the multiplication operation of an opposite ring. $)
    opprmul $p |- ( ( R e. V /\ X e. B /\ Y e. B ) ->
      ( X K Y ) = ( Y T X ) ) $=
      ( vx vy wcel co wceq wa cv cmpt2 opprmulfval oveqd oveq12 ancoms sylan9eq
      eqid ovex ovmpt2a 3impb ) BFOZGAOZHAOZGHDPZHGCPZQUJUKULRUMGHMNAANSZMSZCPZ
      TZPUNUJDURGHMNABCDEFIJKLUAUBMNGHAAUQUNURUOHQUPGQUQUNQUOHUPGCUCUDURUFHGCUG
      UHUEUI $.
      $( [1-Dec-2014] $)
  $}

  ${
    $d s x y z R $.  $d s E $.  $d x y z O $.  $d x y V $.
    opprbase.1 $e |- O = ( oppR ` R ) $.
    ${
      opprlem.2 $e |- E = ( s e. _V |-> ( s ` ( E ` ndx ) ) ) $.
      opprlem.3 $e |- -. ( .r ` ndx ) e. { ( E ` ndx ) } $.
      $( Lemma for ~ opprbase and ~ opprplusg . $)
      opprlem $p |- ( E ` R ) = ( E ` O ) $=
        ( vx vy cvv wcel cfv wceq cnx cmulr cv co csn fvex c0 cbs cop csts snex
        cmpt2 dmsnop eqcom elsnc 3bitr4i mtbi setsnid eqid opprval fveq2d fvprc
        eqtr4d wn coppr syl5eq str0 syl6eq pm2.61i ) AJKZABLZCBLZMVCVDANOLZHIAU
        ALZVGIPHPAOLZQUEZUBZRZUCQZBLVEVFRZVKBJADVJUDVFVIUFFVFNBLZRKZVNVMKZGVFVN
        MVNVFMVOVPVFVNUGVFVNNOSUHVNVFNBSUHUIUJUKVCCVLBHIVGAVHCJVGULVHULEUMUNUPV
        CUQZVDTVEABUOVQVETBLTVQCTBVQCAURLTEAURUOUSUNBVNDFUTVAUPVB $.
        $( [1-Dec-2014] $)
    $}

    $( Base set of an opposite ring. $)
    opprbase $p |- ( Base ` R ) = ( Base ` O ) $=
      ( vx cbs baseid cnx cmulr cfv csn cplusg cpr rnglem snsspr1 sseli opprlem
      wcel mto ) AEBDCDFGHIZGEIZJZQSTGKIZLZQMUAUCSTUBNORP $.
      $( [1-Dec-2014] $)

    $( Addition operation of an opposite ring. $)
    opprplusg $p |- ( +g ` R ) = ( +g ` O ) $=
      ( vx cplusg plusgid cnx cmulr cfv csn wcel cbs cpr rnglem snsspr2 opprlem
      sseli mto ) AEBDCDFGHIZGEIZJZKSGLIZTMZKNUAUCSUBTOQRP $.
      $( [1-Dec-2014] $)

    $( An opposite ring is a ring. $)
    opprrng $p |- ( R e. Ring -> O e. Ring ) $=
      ( vx vy vz crg wcel cbs cfv cplusg cmulr wceq cv co eqid opprmul syl13anc
      syl3anc 3eqtr4d cur opprbase a1i opprplusg eqidd cabel rngabl ablprop w3a
      sylib rngcl 3com23 eqeltrd wa simpl simpr3 simpr2 simpr1 rngass 3adant3r3
      3eqtr4rd oveq1d 3adant3r1 oveq2d rngdir rngacl oveq12d rngdi adantr simpr
      rngidcl rngridm eqtrd rnglidm isrngd ) AGHZDEFAIJZAKJZBBLJZAUAJZVQBIJMVPA
      BCUBZUCVRBKJMVPABCUDZUCVPVSUEVPAUFHBUFHAUGABWAWBUHUJVPDNZVQHZENZVQHZUIWCW
      EVSOZWEWCALJZOZVQVQAWHVSBGWCWEVQPZWHPZCVSPZQZVPWFWDWIVQHZVQAWHWEWCWJWKUKU
      LZUMVPWDWFFNZVQHZUIZUNZWIWPVSOZWCWPWEWHOZVSOZWGWPVSOWCWEWPVSOZVSOWSXAWCWH
      OZWPWIWHOZXBWTWSVPWQWFWDXDXEMVPWRUOZVPWDWFWQUPZVPWDWFWQUQZVPWDWFWQURZVQAW
      HWPWEWCWJWKUSRWSVPWDXAVQHZXBXDMXFXIWSVPWQWFXJXFXGXHVQAWHWPWEWJWKUKSVQAWHV
      SBGWCXAWJWKCWLQSWSVPWNWQWTXEMXFVPWDWFWNWQWOUTXGVQAWHVSBGWIWPWJWKCWLQSVAWS
      WGWIWPVSVPWDWFWGWIMWQWMUTZVBWSXCXAWCVSVPWFWQXCXAMWDVQAWHVSBGWEWPWJWKCWLQV
      CZVDTWSWEWPVROZWCWHOZWIWPWCWHOZVROZWCXMVSOZWGWCWPVSOZVROWSVPWFWQWDXNXPMXF
      XHXGXIVQVRAWHWEWPWCWJVRPZWKVERWSVPWDXMVQHZXQXNMXFXIWSVPWFWQXTXFXHXGVQVRAW
      EWPWJXSVFSVQAWHVSBGWCXMWJWKCWLQSWSWGWIXRXOVRXKWSVPWDWQXRXOMXFXIXGVQAWHVSB
      GWCWPWJWKCWLQSZVGTWSWPWCWEVROZWHOZXOXAVROZYBWPVSOZXRXCVROWSVPWQWDWFYCYDMX
      FXGXIXHVQVRAWHWPWCWEWJXSWKVHRWSVPYBVQHZWQYEYCMXFWSVPWDWFYFXFXIXHVQVRAWCWE
      WJXSVFSXGVQAWHVSBGYBWPWJWKCWLQSWSXRXOXCXAVRYAXLVGTVQAVTWJVTPZVKZVPWDUNZVT
      WCVSOZWCVTWHOZWCYIVPVTVQHZWDYJYKMVPWDUOZVPYLWDYHVIZVPWDVJZVQAWHVSBGVTWCWJ
      WKCWLQSVQAWHVTWCWJWKYGVLVMYIWCVTVSOZVTWCWHOZWCYIVPWDYLYPYQMYMYOYNVQAWHVSB
      GWCVTWJWKCWLQSVQAWHVTWCWJWKYGVNVMVO $.
      $( [1-Dec-2014] $)

    $( Additive identity of an opposite ring. $)
    oppr0 $p |- ( R e. V -> ( 0g ` R ) = ( 0g ` O ) ) $=
      ( vx vy wcel c0g cfv cv cplusg wceq cbs wral crio eqid grpidval cvv coppr
      co fvex eqeltri opprbase opprplusg ax-mp syl6eqr ) ACGAHIZEJFJZAKIZTUHLFA
      MIZNEUJOZBHIZFUJUIEAUGCUJPUIPUGPQBRGULUKLBASIRDASUAUBFUJUIEBULRABDUCABDUD
      ULPQUEUF $.
      $( [1-Dec-2014] $)

    $( Multiplicative identity of an opposite ring. $)
    opprid $p |- ( R e. V -> ( 1r ` R ) = ( 1r ` O ) ) $=
      ( vx vy wcel cur cfv cv cmulr co wceq wral crio eqid rngidval opprmul cvv
      wa cbs wb w3a eqeq1d 3com23 anbi12d 3expa ancom ralbidva riotabidva eqtrd
      syl6rbb coppr fvex eqeltri opprbase ax-mp syl6eqr ) ACGZAHIZEJZFJZBKIZLZV
      BMZVBVAVCLZVBMZTZFAUAIZNZEVIOZBHIZUSUTVAVBAKIZLZVBMZVBVAVMLZVBMZTZFVINZEV
      IOVKFEVIAVMUTCVIPZVMPZUTPQUSVSVJEVIUSVAVIGZTZVRVHFVIWCVBVIGZTVHVQVOTZVRUS
      WBWDVHWEUBUSWBWDUCZVEVQVGVOWFVDVPVBVIAVMVCBCVAVBVTWADVCPZRUDWFVFVNVBUSWDW
      BVFVNMVIAVMVCBCVBVAVTWADWGRUEUDUFUGVQVOUHULUIUJUKBSGVLVKMBAUMISDAUMUNUOFE
      VIBVCVLSABDUPWGVLPQUQUR $.
      $( [1-Dec-2014] $)
  $}

