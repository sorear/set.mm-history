$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Group actions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c GrpAct $.
  $( Extend class definition to include the class of group actions. $)
  cga $a class GrpAct $.

  ${
    $d b g m s x y z G $.  $d m x y z M $.  $d b g m s P $.  $d b g m s U $.
    $d b g m s y z X $.  $d b g m s x y z Y $.
    $( Define the class of all group actions. $)
    df-ga $a |- GrpAct = ( g e. Grp , s e. _V |-> [_ ( Base ` g ) / b ]_
      { m e. ( s ^m ( b X. s ) ) | A. x e. s ( ( ( 0g ` g ) m x ) = x /\
    A. y e. b A. z e. b ( ( y ( +g ` g ) z ) m x ) = ( y m ( z m x ) ) ) } ) $.

    $( The left argument of a group action is a group.  (Contributed by Jeff
       Hankins, 11-Aug-2009.) $)
    gagrp $p |- ( M e. ( G GrpAct Y ) -> G e. Grp ) $=
      ( vg vs vb vx vm vy vz cga co wcel cgrp cvv cxp cfv cv cbs wral cop df-ov
      cdm elfvdm eleq2s c0g wceq cplusg wa cmap crab csb df-ga fvex rabex csbex
      ovex dmmpt2 syl6eleq opelxp1 syl ) BACKLZMZACUAZNOPZMANMVCVDKUCZVEVDVFMBV
      DKQVBBVDKUDACKUBUEDENOFDRZSQZVGUFQGRZHRZLVIUGIRZJRZVGUHQLVIVJLVKVLVIVJLVJ
      LUGJFRZTIVMTUIGERZTZHVNVMVNPZUJLZUKZULKGIJDHEFUMFVHVRVGSUNVOHVQVNVPUJUQUO
      UPURUSACNOUTVA $.
      $( [17-May-2010] $) $( [11-Aug-2009] $)

    isga.1 $e |- X = ( Base ` G ) $.
    isga.2 $e |- P = ( +g ` G ) $.
    isga.3 $e |- U = ( 0g ` G ) $.
    $( The predicate "is a (left) group action."  The group ` G ` is said to
       act on the base set ` Y ` of the action, which is not assumed to have
       any special properties.  There is a related notion of right group
       action, but as the Wikipedia article explains, it is not mathematically
       interesting.  The way actions are usually thought of is that each
       element ` g ` of ` G ` is a permutation of the elements of ` Y ` (see
       ~ gapm ).  Since group theory was classically about symmetry groups, it
       is therefore likely that the notion of group action was useful even in
       early group theory.  (Contributed by Jeff Hankins, 10-Aug-2009.)
       (Revised by Mario Carneiro, 13-Jan-2015.) $)
    isga $p |- ( Y e. V -> ( M e. ( G GrpAct Y ) <-> ( G e. Grp /\
                 M : ( X X. Y ) --> Y /\ A. x e. Y ( ( U M x ) = x /\
      A. y e. X A. z e. X ( ( y P z ) M x ) = ( y M ( z M x ) ) ) ) ) ) $=
      ( vm wcel co wceq wral wa cvv vg vs vb cga cgrp cxp wf w3a gagrp pm4.71ri
      cv cmap crab id elex cbs cfv c0g cplusg csb fvex a1i simplr simpl syl6eqr
      fveq2d sylan9eqr xpeq12d oveq12d simpll oveq1d eqeq1d raleqbidv rabeqbidv
      oveqd anbi12d csbied df-ga ovex rabex ovmpt2a syl2anr eleq2d oveq2d eqtrd
      oveq eqeq12d 2ralbidv ralbidv elrab syl6bb eqeltri sylancr elmapg syl2anc
      wb xpexg anbi1d bitrd pm5.32da syl5bb 3anass syl6bbr ) JHOZGFJUDPZOZFUEOZ
      IJUFZJGUGZEAUKZGPZXJQZBUKZCUKZDPZXJGPZXMXNXJGPZGPZQZCIRBIRZSZAJRZSZSZXGXI
      YBUHXFXGXFSXDYDXFXGFGJUIUJXDXGXFYCXDXGSZXFGJXHULPZOZYBSZYCYEXFGEXJNUKZPZX
      JQZXOXJYIPZXMXNXJYIPZYIPZQZCIRZBIRZSZAJRZNYFUMZOYHYEXEYTGXGXGJTOXEYTQXDXG
      UNJHUOUAUBFJUETUCUAUKZUPUQZUUAURUQZXJYIPZXJQZXMXNUUAUSUQZPZXJYIPZYNQZCUCU
      KZRZBUUJRZSZAUBUKZRZNUUNUUJUUNUFZULPZUMZUTYTUDUUAFQZUUNJQZSZUCUUBUURYTTUU
      BTOUVAUUAUPVAVBUVAUUJUUBQZSZUUOYSNUUQYFUVCUUNJUUPXHULUUSUUTUVBVCZUVCUUJIU
      UNJUVBUVAUUJUUBIUVBUNUVAUUBFUPUQZIUVAUUAFUPUUSUUTVDVFKVEVGZUVDVHVIUVCUUMY
      RAUUNJUVDUVCUUEYKUULYQUVCUUDYJXJUVCUUCEXJYIUVCUUCFURUQEUVCUUAFURUUSUUTUVB
      VJZVFMVEVKVLUVCUUKYPBUUJIUVFUVCUUIYOCUUJIUVFUVCUUHYLYNUVCUUGXOXJYIUVCUUFD
      XMXNUVCUUFFUSUQDUVCUUAFUSUVGVFLVEVOVKVLVMVMVPVMVNVQABCUANUBUCVRYSNYFJXHUL
      VSVTWAWBWCYSYBNGYFYIGQZYRYAAJUVHYKXLYQXTUVHYJXKXJEXJYIGWFVLUVHYOXSBCIIUVH
      YLXPYNXRXOXJYIGWFUVHYNXMYMGPXRXMYMYIGWFUVHYMXQXMGXNXJYIGWFWDWEWGWHVPWIWJW
      KYEYGXIYBYEXDXHTOZYGXIWPXDXGVDZYEITOXDUVIIUVETKFUPVAWLUVJIJTHWQWMJXHGHTWN
      WOWRWSWTXAXGXIYBXBXC $.
      $( [13-Jan-2015] $) $( [10-Aug-2009] $)
  $}

  ${
    $d x A $.  $d x y z G $.  $d x y z M $.  $d x U $.  $d x y z Y $.
    gagrpid.1 $e |- U = ( 0g ` G ) $.
    $( The identity of the group does not alter the base set.  (Contributed by
       Jeff Hankins, 11-Aug-2009.) $)
    gagrpid $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ A e. Y ) ->
      ( U M A ) = A ) $=
      ( vx vy vz cga co wcel wceq wa cv wral cfv eqid syl wi cplusg cbs cgrp wf
      cxp w3a biimpa ancoms simp3d simpl ralimi oveq2 id eqeq12d rcla4cv 3impia
      isga ) DCFKLMZFEMZAFMZBADLZANZUSUTOZBHPZDLZVENZHFQZVAVCUAVDVGIPZJPZCUBRZL
      VEDLVIVJVEDLDLNJCUCRZQIVLQZOZHFQZVHVDCUDMZVLFUFFDUEZVOUTUSVPVQVOUGZUTUSVR
      HIJVKBCDEVLFVLSVKSGURUHUIUJVNVGHFVGVMUKULTVGVCHAFVEANZVFVBVEAVEABDUMVSUNU
      OUPTUQ $.
      $( [13-Jan-2015] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z M $.  $d x V $.  $d x y z X $.  $d x y z Y $.
    gaf.1 $e |- X = ( Base ` G ) $.
    $( The mapping of the group action operation.  (Contributed by Jeff
       Hankins, 11-Aug-2009.) $)
    gaf $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V ) -> M : ( X X. Y ) --> Y ) $=
      ( vx vy vz cga co wcel wa cgrp cxp cfv cv wceq wral eqid wf cplusg biimpa
      c0g w3a isga ancoms simp2d ) BAEJKLZECLZMANLZDEOEBUAZAUDPZGQZBKUNRHQZIQZA
      UBPZKUNBKUOUPUNBKBKRIDSHDSMGESZUJUIUKULURUEZUJUIUSGHIUQUMABCDEFUQTUMTUFUC
      UGUH $.
      $( [13-Jan-2015] $) $( [11-Aug-2009] $)

    $( A group action is onto its base set.  (Contributed by Jeff Hankins,
       10-Aug-2009.) $)
    gafo $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V ) ->
                 M : ( X X. Y ) -onto-> Y ) $=
      ( vx vy vz cga co wcel wa cxp wf cv wceq wrex wral eqeq2d wfo gaf c0g cfv
      cgrp gagrp ad2antrr grpidcl syl simpr gagrpid 3expa eqcomd oveq1 rcla42ev
      eqid oveq2 syl3anc ralrimiva foov sylanbrc ) BAEJKLZECLZMZDENZEBOGPZHPZIP
      ZBKZQZIERHDRZGESVEEBUAABCDEFUBVDVKGEVDVFELZMZAUCUDZDLZVLVFVNVFBKZQZVKVMAU
      ELZVOVBVRVCVLABEUFUGDAVNFVNUPZUHUIVDVLUJVMVPVFVBVCVLVPVFQVFVNABCEVSUKULUM
      VJVQVFVNVHBKZQHIVNVFDEVGVNQVIVTVFVGVNVHBUNTVHVFQVTVPVFVHVFVNBUQTUOURUSHIG
      DEEBUTVA $.
      $( [13-Jan-2015] $) $( [10-Aug-2009] $)
  $}

  ${
    $d x y z C $.  $d x y z G $.  $d x y z M $.  $d x y z P $.  $d x y z X $.
    $d y z A $.  $d z B $.  $d x y z Y $.
    gaass.1 $e |- X = ( Base ` G ) $.
    gaass.2 $e |- P = ( +g ` G ) $.
    $( An "associative" property for group actions.  (Contributed by Jeff
       Hankins, 11-Aug-2009.) $)
    gaass $p |- ( ( ( M e. ( G GrpAct Y ) /\ Y e. V ) /\
      ( A e. X /\ B e. X /\ C e. Y ) ) ->
        ( ( A P B ) M C ) = ( A M ( B M C ) ) ) $=
      ( vy vz vx wcel w3a co wceq cv wral cga wa wi c0g cfv cgrp cxp wf biimpac
      eqid isga simp3d simpr ralimi syl oveq2 oveq2d eqeq12d oveq1 rcla43v syl5
      oveq1d 3coml impcom ) AHOZBHOZCIOZPFEIUAQOZIGOZUBZABDQZCFQZABCFQZFQZRZVGV
      EVFVJVOUCVJLSZMSZDQZNSZFQZVPVQVSFQZFQZRZMHTLHTZNITZVGVEVFPVOVJEUDUEZVSFQV
      SRZWDUBZNITZWEVJEUFOZHIUGIFUHZWIVIVHWJWKWIPNLMDWFEFGHIJKWFUJUKUIULWHWDNIW
      GWDUMUNUOWCVOVRCFQZVPVQCFQZFQZRAVQDQZCFQZAWMFQZRNLMCABIHHVSCRZVTWLWBWNVSC
      VRFUPWRWAWMVPFVSCVQFUPUQURVPARZWLWPWNWQWSVRWOCFVPAVQDUSVBVPAWMFUSURVQBRZW
      PVLWQVNWTWOVKCFVQBADUPVBWTWMVMAFVQBCFUSUQURUTVAVCVD $.
      $( [13-Jan-2015] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z G $.
    $( The action of a group on the empty set.  (Contributed by Jeff Hankins,
       11-Aug-2009.) $)
    ga0 $p |- ( G e. Grp -> (/) e. ( G GrpAct (/) ) ) $=
      ( vx vy vz cgrp wcel cbs cfv c0 cxp wf c0g cv co wceq cplusg wral a1i cvv
      eqid wa cga id f0 xp0 feq2i mpbir ral0 w3a wb 0ex isga ax-mp syl3anbrc )
      AEFZUOAGHZIJZIIKZALHZBMZINUTOCMZDMZAPHZNUTINVAVBUTININODUPQCUPQUAZBIQZIAI
      UBNFZUOUCURUOURIIIKIUDUQIIIUPUEUFUGRVEUOVDBUHRISFVFUOURVEUIUJUKBCDVCUSAIS
      UPIUPTVCTUSTULUMUN $.
      $( [13-Jan-2015] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y z G $.  $d x y z S $.  $d x y z V $.  $d x y z X $.
    gaid.1 $e |- X = ( Base ` G ) $.
    $( The trivial action of a group on any set.  Each group element
       corresponds to the identity permutation.  (Contributed by Jeff Hankins,
       11-Aug-2009.)  (The proof was shortened by Mario Carneiro,
       13-Jan-2015.) $)
    gaid $p |- ( ( G e. Grp /\ S e. V ) ->
      ( 2nd |` ( X X. S ) ) e. ( G GrpAct S ) ) $=
      ( vx vy vz wcel wa c2nd co cfv wceq ovres cop df-ov op2nd eqtri syl6eq wf
      cgrp cxp cres cga c0g cv cplusg wral f2ndres a1i eqid grpidcl adantr fvex
      simpl vex sylan simprl simplr syl2anc simprr oveq2d simpll grpcl 3eqtr4rd
      3expb ovex ralrimivva jca ralrimiva w3a wb isga adantl mpbir3and ) BUBIZA
      CIZJZKDAUCZUDZBAUELIZVQVTAWAUAZBUFMZFUGZWALZWENZGUGZHUGZBUHMZLZWEWALZWHWI
      WEWALZWALZNZHDUIGDUIZJZFAUIZVQVRUPWCVSDAUJUKVSWQFAVSWEAIZJZWGWPVSWDDIZWSW
      GVQXAVRDBWDEWDULZUMUNXAWSJWFWDWEKLZWEWDWEDAKOXCWDWEPKMWEWDWEKQWDWEBUFUOFU
      QZRSTURWTWOGHDDWTWHDIZWIDIZJZJZWHWEWALZWEWNWLXHXEWSXIWENWTXEXFUSVSWSXGUTZ
      XEWSJXIWHWEKLZWEWHWEDAKOXKWHWEPKMWEWHWEKQWHWEGUQXDRSTVAXHWMWEWHWAXHXFWSWM
      WENWTXEXFVBXJXFWSJWMWIWEKLZWEWIWEDAKOXLWIWEPKMWEWIWEKQWIWEHUQXDRSTVAVCXHW
      KDIZWSWLWENWTVQXGXMVQVRWSVDVQXEXFXMDWJBWHWIEWJULZVEVGURXJXMWSJWLWKWEKLZWE
      WKWEDAKOXOWKWEPKMWEWKWEKQWKWEWHWIWJVHXDRSTVAVFVIVJVKVRWBVQWCWRVLVMVQFGHWJ
      WDBWACDAEXNXBVNVOVP $.
      $( [13-Jan-2015] $) $( [11-Aug-2009] $)
  $}

  ${
    $d u v w F $.  $d u v w x y G $.  $d u v w x y X $.  $d u v w x y Y $.
    $d u v w H $.  $d x y P $.
    subgga.1 $e |- X = ( Base ` G ) $.
    subgga.2 $e |- P = ( +g ` G ) $.
    subgga.3 $e |- H = ( G |`s Y ) $.
    subgga.4 $e |- F = ( x e. Y , y e. X |-> ( x P y ) ) $.
    $( A subgroup acts on its parent group.  (Contributed by Jeff Hankins,
       13-Aug-2009.)  (The proof was shortened by Mario Carneiro,
       13-Jan-2015.) $)
    subgga $p |- ( Y e. ( SubGrp ` G ) -> F e. ( H GrpAct X ) ) $=
      ( vv vw cfv wcel co wceq wral wa vu csubg cgrp cbs cxp wf c0g cga subggrp
      subgrcl adantr subgss sselda adantrr grpcl syl3anc ralrimivva fmpt2 sylib
      cv simprr subgbas xpeq1d feq2d mpbid eqid subg0cl oveq12 ovex sylan subg0
      ovmpt2a oveq1d grplid 3eqtr3d ad2antrr wss simprl sseldd syl13anc syl2anc
      simplr grpass eqtr4d subgcl 3expb adantlr oveq2d 3eqtr4d raleqbidv biimpa
      raleqdv syldan jca ralrimiva cvv w3a wb fvex eqeltri ressplusg isga ax-mp
      syl3anbrc ) HEUBOPZFUCPZFUDOZGUEZGDUFZFUGOZUAUTZDQZXKRZMUTZNUTZCQZXKDQZXN
      XOXKDQZDQZRZNXGSZMXGSZTZUAGSZDFGUHQPZHEFKUIXEHGUEZGDUFZXIXEAUTZBUTZCQZGPZ
      BGSAHSYGXEYKABHGXEYHHPZYIGPZTZTEUCPZYHGPZYMYKXEYOYNHEUJZUKXEYLYPYMXEHGYHG
      HEIULZUMUNXEYLYMVAGCEYHYIIJUOUPUQABHGYJGDLURUSXEYFXHGDXEHXGGHEFKVBZVCVDVE
      XEYCUAGXEXKGPZTZXMYBUUAEUGOZXKDQZUUBXKCQZXLXKXEUUBHPYTUUCUUDRHEUUBUUBVFZV
      GABUUBXKHGYJUUDDYHUUBYIXKCVHLUUBXKCVIVLVJXEUUCXLRYTXEUUBXJXKDHEFUUBKUUEVK
      VMUKXEYOYTUUDXKRYQGCEUUBXKIJUUEVNVJVOXEYTXTNHSZMHSZYBUUAXTMNHHUUAXNHPZXOH
      PZTZTZXPXKCQZXNXOXKCQZDQZXQXSUUKUULXNUUMCQZUUNUUKYOXNGPXOGPZYTUULUUORXEYO
      YTUUJYQVPZUUKHGXNXEHGVQYTUUJYRVPZUUAUUHUUIVRZVSUUKHGXOUURUUAUUHUUIVAZVSZX
      EYTUUJWBZGCEXNXOXKIJWCVTUUKUUHUUMGPZUUNUUORUUSUUKYOUUPYTUVCUUQUVAUVBGCEXO
      XKIJUOUPABXNUUMHGYJUUODYHXNYIUUMCVHLXNUUMCVIVLWAWDUUKXPHPZYTXQUULRXEUUJUV
      DYTXEUUHUUIUVDCHEXNXOJWEWFWGUVBABXPXKHGYJUULDYHXPYIXKCVHLXPXKCVIVLWAUUKXR
      UUMXNDUUKUUIYTXRUUMRUUTUVBABXOXKHGYJUUMDYHXOYIXKCVHLXOXKCVIVLWAWHWIUQXEUU
      GYBXEUUFYAMHXGYSXEXTNHXGYSWLWJWKWMWNWOGWPPYEXFXIYDWQWRGEUDOWPIEUDWSWTUAMN
      CXJFDWPXGGXGVFHCEFKJXAXJVFXBXCXD $.
      $( [13-Jan-2015] $) $( [13-Aug-2009] $)
  $}

  ${
    $d u v x y z G $.  $d u v x y z M $.  $d u v x y z V $.  $d u v x y z X $.
    $d u v x y z Y $.  $d u v x y z Z $.
    gass.1 $e |- X = ( Base ` G ) $.
    $( A subset of a group action is a group action iff it is closed under the
       group action operation. $)
    gass $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ Z C_ Y ) ->
      ( ( M |` ( X X. Z ) ) e. ( G GrpAct Z ) <->
        A. x e. X A. y e. Z ( x M y ) e. Z ) ) $=
      ( vz vu co wcel cv wral wa wceq ovres syl2anc syl vv cga wss w3a cxp cres
      adantl wf cvv simp3 simp2 ssexg gaf sylan fovrn 3expb eqeltrrd ralrimivva
      ancoms cgrp c0g cfv cplusg simpl1 gagrp wfn simpl2 jca ffn simpl3 fnssres
      xpss2 simpr eleq1d ralbidva ralbiia sylibr ffnov sylanbrc grpidcl gagrpid
      eqid sselda 3expa syldan eqtrd ad2antrr simprl simprr gaass simpllr oveq1
      adantr syl13anc rcla42va syl21anc eqtr4d syl3anc oveq2d 3eqtr4d ralrimiva
      oveq2 grpcl wb isga mpbir3and impbida ) DCGUBLMZGEMZHGUCZUDZDFHUEZUFZCHUB
      LMZANZBNZDLZHMZBHOZAFOZXKXNPZXRABFHYAXOFMZXPHMZPZPXOXPXMLZXQHYDYEXQQYAXOX
      PFHDRZUGYAXLHXMUHZYDYEHMZXKHUIMZXNYGXKXJXIYIXHXIXJUJXHXIXJUKHGEULSZXNYIYG
      CXMUIFHIUMUSUNYGYBYCYHXOXPHFHXMUOUPUNUQURXKXTPZXNCUTMZYGCVAVBZJNZXMLZYNQZ
      KNZUANZCVCVBZLZYNXMLZYQYRYNXMLZXMLZQZUAFOKFOZPZJHOZYKXHYLXHXIXJXTVDZCDGVE
      TZYKXMXLVFZYHBHOZAFOZYGYKDFGUEZVFZXLUUMUCZUUJYKUUMGDUHZUUNYKXHXIPZUUPYKXH
      XIUUHXHXIXJXTVGVHZCDEFGIUMTUUMGDVITYKXJUUOXHXIXJXTVJZHGFVLTUUMXLDVKSYKXTU
      ULXKXTVMUUKXSAFYBYHXRBHYDYEXQHYFVNVOVPVQABFHHXMVRVSYKUUFJHYKYNHMZPZYPUUEU
      VAYOYMYNDLZYNYKYMFMZUUTYOUVBQYKYLUVCUUIFCYMIYMWBZVTTYMYNFHDRUNYKUUTYNGMZU
      VBYNQZYKHGYNUUSWCZYKUUQUVEUVFUURXHXIUVEUVFYNYMCDEGUVDWAWDUNWEWFUVAUUDKUAF
      FUVAYQFMZYRFMZPZPZYTYNDLZYQYRYNDLZXMLZUUAUUCUVKUVLYQUVMDLZUVNUVKUUQUVHUVI
      UVEUVLUVOQYKUUQUUTUVJUURWGUVAUVHUVIWHZUVAUVHUVIWIZUVAUVEUVJUVGWMYQYRYNYSC
      DEFGIYSWBZWJWNUVKUVHUVMHMZUVNUVOQUVPUVKUVIUUTXTUVSUVQUVAUUTUVJYKUUTVMWMZX
      KXTUUTUVJWKXRUVSYRXPDLZHMABYRYNFHXOYRQXQUWAHXOYRXPDWLVNXPYNQUWAUVMHXPYNYR
      DXBVNWOWPYQUVMFHDRSWQUVKYTFMZUUTUUAUVLQUVKYLUVHUVIUWBYKYLUUTUVJUUIWGUVPUV
      QFYSCYQYRIUVRXCWRUVTYTYNFHDRSUVKUUBUVMYQXMUVKUVIUUTUUBUVMQUVQUVTYRYNFHDRS
      WSWTURVHXAYKYIXNYLYGUUGUDXDXKYIXTYJWMJKUAYSYMCXMUIFHIUVRUVDXETXFXG $.
      $( [17-Jan-2015] $)
  $}

  ${
    $d x y z G $.  $d x y z H $.  $d x y z M $.  $d x y z S $.  $d x y z V $.
    $d x y z Y $.
    gasubg.1 $e |- H = ( G |`s S ) $.
    $( The restriction of a group action to a subgroup is a group action. $)
    gasubg $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ S e. ( SubGrp ` G ) ) ->
      ( M |` ( S X. Y ) ) e. ( H GrpAct Y ) ) $=
      ( vx vy vz co wcel cfv cxp wceq wa syl eqid syl2anc ovres csubg cres cgrp
      cga w3a cbs wf c0g cplusg wral simp3 subggrp wss gaf 3adant3 subgss xpss1
      fssres subgbas xpeq1d feq2d mpbid adantr simpr subg0 oveq1d simpl1 simpl2
      subg0cl gagrpid syl3anc 3eqtr3d eqimss2 sselda anim12dan simpll1 ad2antrr
      cv simpll2 simprl sseldd simprr gaass syl23anc fovrn eqtr4d subgcl oveq2d
      3expb sylan 3eqtr4d syldan ralrimivva ralrimiva simp2 ressplusg mpbir3and
      jca wb isga ) DBFUDKLZFELZABUAMLZUEZDAFNZUBZCFUDKLZCUCLZCUFMZFNZFXFUGZCUH
      MZHVRZXFKZXMOZIVRZJVRZBUIMZKZXMXFKZXPXQXMXFKZXFKZOZJXIUJIXIUJZPZHFUJZXDXC
      XHXAXBXCUKZABCGULQXDXEFXFUGZXKXDBUFMZFNZFDUGZXEYJUMZYHXAXBYKXCBDEYIFYIRZU
      NUOZXDAYIUMZYLXDXCYOYGYIABYMUPQZAYIFUQQYJFXEDURSXDXEXJFXFXDAXIFXDXCAXIOZY
      GABCGUSQZUTVAVBXDYEHFXDXMFLZPZXOYDYTBUHMZXMXFKZUUAXMDKZXNXMYTUUAALZYSUUBU
      UCOYTXCUUDXDXCYSYGVCZABUUAUUARZVIQXDYSVDZUUAXMAFDTSYTUUAXLXMXFYTXCUUAXLOU
      UEABCUUAGUUFVEQVFYTXAXBYSUUCXMOXAXBXCYSVGXAXBXCYSVHUUGXMUUABDEFUUFVJVKVLY
      TYCIJXIXIYTXPXILZXQXILZPXPALZXQALZPZYCYTUUHUUJUUIUUKYTXIAXPXDXIAUMZYSXDYQ
      UUMYRXIAVMQVCZVNYTXIAXQUUNVNVOYTUULPZXSXMDKZXPXQXMDKZXFKZXTYBUUOUUPXPUUQD
      KZUURUUOXAXBXPYILXQYILZYSUUPUUSOXAXBXCYSUULVPXAXBXCYSUULVSUUOAYIXPXDYOYSU
      ULYPVQZYTUUJUUKVTZWAUUOAYIXQUVAYTUUJUUKWBZWAZYTYSUULUUGVCZXPXQXMXRBDEYIFY
      MXRRZWCWDUUOUUJUUQFLZUURUUSOUVBUUOYKUUTYSUVGXDYKYSUULYNVQUVDUVEXQXMFYIFDW
      EVKXPUUQAFDTSWFUUOXSALZYSXTUUPOYTXCUULUVHUUEXCUUJUUKUVHXRABXPXQUVFWGWIWJU
      VEXSXMAFDTSUUOYAUUQXPXFUUOUUKYSYAUUQOUVCUVEXQXMAFDTSWHWKWLWMWRWNXDXBXGXHX
      KYFUEWSXAXBXCWOHIJXRXLCXFEXIFXIRAXRBCGUVFWPXLRWTQWQ $.
      $( [17-Jan-2015] $)
  $}

  ${
    $d x y G $.  $d x y P $.  $d x y X $.
    gaid2.1 $e |- X = ( Base ` G ) $.
    gaid2.2 $e |- P = ( +g ` G ) $.
    gaid2.3 $e |- F = ( x e. X , y e. X |-> ( x P y ) ) $.
    $( A group operation is a left group action of the group on itself.
       (Contributed by FL, 17-May-2010.)  (Revised by Mario Carneiro,
       13-Jan-2015.) $)
    gaid2 $p |- ( G e. Grp -> F e. ( G GrpAct X ) ) $=
      ( cgrp wcel cress co cga csubg cfv subgid eqid subgga syl ressid eleqtrd
      oveq1d ) EJKZDEFLMZFNMZEFNMUDFEOPKDUFKFEGQABCDEUEFFGHUERISTUDUEEFNFEJGUAU
      CUB $.
      $( [13-Jan-2015] $) $( [17-May-2010] $)
  $}

  ${
    galcan.1 $e |- X = ( Base ` G ) $.
    $( The action of a particular group element is left-cancelable.
       (Contributed by FL, 17-May-2010.)  (Revised by Mario Carneiro,
       13-Jan-2015.) $)
    galcan $p |- ( ( ( M e. ( G GrpAct Y ) /\ Y e. V ) /\
      ( A e. X /\ B e. Y /\ C e. Y ) ) ->
        ( ( A M B ) = ( A M C ) <-> B = C ) ) $=
      ( co wcel wa wceq cfv oveq2 eqid syl2anc oveq1d gaass syl23anc cga cplusg
      w3a cminusg c0g cgrp simpll simpr1 grplinv simplr grpinvcl simpr2 gagrpid
      gagrp syl syl3anc 3eqtr3d simpr3 eqeq12d syl5ib impbid1 ) EDHUAJKZHFKZLZA
      GKZBHKZCHKZUCZLZABEJZACEJZMZBCMZVLADUDNZNZVJEJZVOVKEJZMVIVMVJVKVOEOVIVPBV
      QCVIVOADUBNZJZBEJZDUENZBEJZVPBVIVSWABEVIDUFKZVEVSWAMVIVBWCVBVCVHUGZDEHUNU
      OZVDVEVFVGUHZGVRDVNWAAIVRPZWAPZVNPZUIQZRVIVBVCVOGKZVEVFVTVPMWDVBVCVHUJZVI
      WCVEWKWEWFGDVNAIWIUKQZWFVDVEVFVGULZVOABVRDEFGHIWGSTVIVBVCVFWBBMWDWLWNBWAD
      EFHWHUMUPUQVIVSCEJZWACEJZVQCVIVSWACEWJRVIVBVCWKVEVGWOVQMWDWLWMWFVDVEVFVGU
      RZVOACVRDEFGHIWGSTVIVBVCVGWPCMWDWLWQCWADEFHWHUMUPUQUSUTBCAEOVA $.
      $( [13-Jan-2015] $) $( [17-May-2010] $)

    gacan.2 $e |- N = ( invg ` G ) $.
    $( Group inverses cancel in a group action.  (Contributed by Jeff Hankins,
       11-Aug-2009.) $)
    gacan $p |- ( ( ( M e. ( G GrpAct Y ) /\ Y e. V ) /\
      ( A e. X /\ B e. Y /\ C e. Y ) ) ->
        ( ( A M B ) = C <-> ( ( N ` A ) M C ) = B ) ) $=
      ( co wcel wa wceq cfv eqid syl2anc syl23anc syl3anc cga w3a cplusg simpll
      c0g cgrp gagrp simpr1 grprinv oveq1d simplr grpinvcl simpr3 gaass gagrpid
      syl 3eqtr3d eqeq2d wb simpr2 cxp wf gaf adantr fovrn galcan bitr3d syl6bb
      eqcom ) EDIUALMZIGMZNZAHMZBIMZCIMZUBZNZABELZCOZBAFPZCELZOZWABOVQVRAWAELZO
      ZVSWBVQWCCVRVQAVTDUCPZLZCELZDUEPZCELZWCCVQWFWHCEVQDUFMZVMWFWHOVQVJWJVJVKV
      PUDZDEIUGUPZVLVMVNVOUHZHWEDFWHAJWEQZWHQZKUIRUJVQVJVKVMVTHMZVOWGWCOWKVJVKV
      PUKZWMVQWJVMWPWLWMHDFAJKULRZVLVMVNVOUMZAVTCWEDEGHIJWNUNSVQVJVKVOWICOWKWQW
      SCWHDEGIWOUOTUQURVQVJVKVMVNWAIMZWDWBUSWKWQWMVLVMVNVOUTVQHIVAIEVBZWPVOWTVL
      XAVPDEGHIJVCVDWRWSVTCIHIEVETABWADEGHIJVFSVGBWAVIVH $.
      $( [13-Jan-2015] $) $( [11-Aug-2009] $)
  $}

  ${
    $d x y A $.  $d x y G $.  $d x y M $.  $d x y V $.  $d x y X $.
    $d x y Y $.
    gapm.1 $e |- X = ( Base ` G ) $.
    gapm.2 $e |- F = ( x e. Y |-> ( A M x ) ) $.
    $( The action of a particular group element is a permutation of the base
       set.  (Contributed by Jeff Hankins, 11-Aug-2009.)  (The proof was
       shortened by Mario Carneiro, 13-Jan-2015.) $)
    gapm $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ A e. X ) ->
      F : Y -1-1-onto-> Y ) $=
      ( vy co wcel cv cfv wa adantr simpl3 simpr wceq cga w3a cminusg cxp fovrn
      wf gaf 3adant3 syl3anc cgrp simpl1 gagrp syl eqid grpinvcl syl2anc simpl2
      wb simprl simprr gacan syl23anc bicomd eqcom 3bitr4g f1o2d ) EDHUALMZHFMZ
      BGMZUBZAKHHBANZELZBDUCOZOZKNZELZCJVJVKHMZPGHUDHEUFZVIVQVLHMVJVRVQVGVHVRVI
      DEFGHIUGUHZQVGVHVIVQRVJVQSBVKHGHEUEUIVJVOHMZPZVRVNGMZVTVPHMVJVRVTVSQWADUJ
      MZVIWBWAVGWCVGVHVIVTUKDEHULUMVGVHVIVTRGDVMBIVMUNZUOUPVJVTSVNVOHGHEUEUIVJV
      QVTPZPZVPVKTZVLVOTZVKVPTVOVLTWFWHWGWFVGVHVIVQVTWHWGURVGVHVIWEUKVGVHVIWEUQ
      VGVHVIWERVJVQVTUSVJVQVTUTBVKVODEVMFGHIWDVAVBVCVKVPVDVOVLVDVEVF $.
      $( [13-Jan-2015] $) $( [11-Aug-2009] $)
  $}

  ${
    $d w x y z G $.  $d w x y z M $.  $d w x y z V $.  $d w x y z X $.
    $d w z F $.  $d w x z H $.  $d w x y z Y $.
    galactghm.x $e |- X = ( Base ` G ) $.
    galactghm.h $e |- H = ( SymGrp ` Y ) $.
    galactghm.f $e |- F = ( x e. X |-> ( y e. Y |-> ( x M y ) ) ) $.
    $( The currying of a group action is a group homomorphism between the group
       ` G ` and the symetry group ` ( SymGrp `` Y ) ` .  (Contributed by FL,
       17-May-2010.)  (The proof was shortened by Mario Carneiro,
       13-Jan-2015.) $)
    galactghm $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V ) ->
      F e. ( G GrpHom H ) ) $=
      ( co wcel wa cfv wceq cmpt cvv syl2anc vz vw cga cgrp cbs wf cv wral cghm
      cplusg gagrp symggrp anim12i wf1o eqid 3expa wb elsymgbas ad2antlr mpbird
      gapm fmptd df-3an gaass sylan2br anassrs mpteq2dva ad2antrr simprl simprr
      w3a grpcl syl3anc mptexg oveq1 mpteq2dv fvmptg ccom simplr adantr ffvelrn
      symgov cxp gaf simpr fovrn oveq2 cbvmptv syl6eq fmptco 3eqtr4d ralrimivva
      eqtrd jca csymg fvex eqeltri isghm ax-mp sylanbrc ) FDIUCMNZIGNZOZDUDNZEU
      DNZOZHEUEPZCUFZUAUGZUBUGZDUJPZMZCPZXICPZXJCPZEUJPZMZQZUBHUHUAHUHZOZCDEUIM
      NZXAXDXBXEDFIUKZIEGKULUMXCXHXSXCAHBIAUGZBUGZFMZRZXGCXCYCHNZOYFXGNZIIYFUNZ
      XAXBYGYIBYCYFDFGHIJYFUOVAUPXBYHYIUQXAYGIXGYFEGKXGUOZURUSUTLVBZXCXRUAUBHHX
      CXIHNZXJHNZOZOZBIXLYDFMZRZBIXIXJYDFMZFMZRZXMXQYOBIYPYSXCYNYDINZYPYSQZYNUU
      AOXCYLYMUUAVKUUBYLYMUUAVCXIXJYDXKDFGHIJXKUOZVDVEVFVGYOXLHNZYQSNZXMYQQYOXD
      YLYMUUDXAXDXBYNYBVHXCYLYMVIZXCYLYMVJZHXKDXIXJJUUCVLVMXBUUEXAYNBIYPGVNUSAX
      LYFYQHSCYCXLQBIYEYPYCXLYDFVOVPLVQTYOXQXNXOVRZYTYOXBXNXGNZXOXGNZXQUUHQXAXB
      YNVSYOXHYLUUIXCXHYNYKVTZUUFHXGXICWATYOXHYMUUJUUKUUGHXGXJCWATIXGXPEGXNXOKY
      JXPUOZWBVMYOBAIIYRXIYCFMZYSXOXNYOUUAOHIWCIFUFZYMUUAYRINXCUUNYNUUADFGHIJWD
      VHYOYMUUAUUGVTYOUUAWEXJYDIHIFWFVMYOYMBIYRRZSNZXOUUOQUUGXBUUPXAYNBIYRGVNUS
      AXJYFUUOHSCYCXJQBIYEYRYCXJYDFVOVPLVQTYOXNBIXIYDFMZRZAIUUMRYOYLUURSNZXNUUR
      QUUFXBUUSXAYNBIUUQGVNUSAXIYFUURHSCYCXIQBIYEUUQYCXIYDFVOVPLVQTBAIUUQUUMYDY
      CXIFWGWHWIYCYRXIFWGWJWMWKWLWNESNYAXFXTOUQEIWOPSKIWOWPWQUBUAXKXPDECSHXGJYJ
      UUCUULWRWSWT $.
      $( [13-Jan-2015] $) $( [17-May-2010] $)
  $}

  ${
    $d u v x y z F $.  $d u v x y z G $.  $d u v x y z H $.  $d u v x y z V $.
    $d u v z M $.  $d u v x y X $.  $d u v x y z Y $.
    lactghmga.x $e |- X = ( Base ` G ) $.
    lactghmga.h $e |- H = ( SymGrp ` Y ) $.
    lactghmga.f $e |- M = ( x e. X , y e. Y |-> ( ( F ` x ) ` y ) ) $.
    $( The converse of ~ galactghm .  The uncurrying of a homomorphism into
       ` ( SymGrp `` Y ) ` is a group action.  Thus group actions and group
       homomorphisms into a symmetric group are essentially equivalent
       notions.  (Contributed by Mario Carneiro, 15-Jan-2015.) $)
    lactghmga $p |- ( ( F e. ( G GrpHom H ) /\ Y e. V ) ->
      M e. ( G GrpAct Y ) ) $=
      ( co wcel wa cfv wceq fveq2 fveq1d syl2anc vz vu vv cghm cga cgrp cxp c0g
      wf cv cplusg wral ghmgrp1 adantr wf1o cbs symggrp eqid ghmf ffvelrn sylan
      sylan2 wb elsymgbas ad2antlr mpbid f1of syl ralrimiva fmpt2 sylib grpidcl
      cid cres simpr fvex ovmpt2 symgid eqtr4d fvresi adantl 3eqtrd ccom simprr
      ghmid ad2antrr simpllr fvco3 simplll simprl ghmlin syl22anc syl3anc eqtrd
      symgov 3eqtr4d grpcl oveq2d ralrimivva jca w3a isga mpbir3and ) CDEUDMNZI
      GNZOZFDIUEMNZDUFNZHIUGIFUIZDUHPZUAUJZFMZXKQZUBUJZUCUJZDUKPZMZXKFMZXNXOXKF
      MZFMZQZUCHULUBHULZOZUAIULZXDXHXEDECUMUNZXFBUJZAUJZCPZPZINZBIULZAHULXIXFYK
      AHXFYGHNZOZYJBIYMIIYHUIZYFINYJYMIIYHUOZYNYMYHEUPPZNZYOXFHYPCUIZYLYQXEXDEU
      FNZYRIEGKUQZDECUFHYPJYPURZUSVBZHYPYGCUTVAXEYQYOVCXDYLIYPYHEGKUUAVDVEVFIIY
      HVGVHIIYFYHUTVAVIVIABHIYIIFLVJVKXFYCUAIXFXKINZOZXMYBUUDXLXKXJCPZPZXKVMIVN
      ZPZXKUUDXJHNZUUCXLUUFQXFUUIUUCXFXHUUIYEHDXJJXJURZVLVHUNXFUUCVOZABXJXKHIYI
      UUFFYFUUEPYGXJQYFYHUUEYGXJCRSYFXKUUERLXKUUEVPVQTUUDXKUUEUUGUUDUUEEUHPZUUG
      XFUUEUULQZUUCXEXDYSUUMYTDECUFXJUULUUJUULURWEVBUNXEUUGUULQXDUUCIEGKVRVEVSS
      UUCUUHXKQXFIXKVTWAWBUUDYAUBUCHHUUDXNHNZXOHNZOZOZXKXQCPZPZXNXKXOCPZPZFMZXR
      XTUUQXKXNCPZUUTWCZPZUVAUVCPZUUSUVBUUQIIUUTUIZUUCUVEUVFQUUQIIUUTUOZUVGUUQU
      UTYPNZUVHUUQYRUUOUVIXFYRUUCUUPUUBWFZUUDUUNUUOWDZHYPXOCUTTZUUQXEUVIUVHVCXD
      XEUUCUUPWGZIYPUUTEGKUUAVDVHVFIIUUTVGVHZUUDUUCUUPUUKUNZIIXKUVCUUTWHTUUQXKU
      URUVDUUQUURUVCUUTEUKPZMZUVDUUQXDYSUUNUUOUURUVQQXDXEUUCUUPWIUUQXEYSUVMYTVH
      UUDUUNUUOWJZUVKXPUVPDEXNCXOUFHJXPURZUVPURZWKWLUUQXEUVCYPNZUVIUVQUVDQUVMUU
      QYRUUNUWAUVJUVRHYPXNCUTTUVLIYPUVPEGUVCUUTKUUAUVTWOWMWNSUUQUUNUVAINZUVBUVF
      QUVRUUQUVGUUCUWBUVNUVOIIXKUUTUTTABXNUVAHIYIUVFFYFUVCPYGXNQYFYHUVCYGXNCRSY
      FUVAUVCRLUVAUVCVPVQTWPUUQXQHNZUUCXRUUSQUUQXHUUNUUOUWCXFXHUUCUUPYEWFUVRUVK
      HXPDXNXOJUVSWQWMUVOABXQXKHIYIUUSFYFUURPYGXQQYFYHUURYGXQCRSYFXKUURRLXKUURV
      PVQTUUQXSUVAXNFUUQUUOUUCXSUVAQUVKUVOABXOXKHIYIUVAFYFUUTPYGXOQYFYHUUTYGXOC
      RSYFXKUUTRLXKUUTVPVQTWRWPWSWTVIXEXGXHXIYDXAVCXDUAUBUCXPXJDFGHIJUVSUUJXBWA
      XC $.
      $( [15-Jan-2015] $)
  $}

  ${
    $d a g x y G $.  $d g x H $.  $d a g x y P $.  $d a x U $.  $d a g x y X $.
    $d x F $.  $d x S $.
    cayleylem1.x $e |- X = ( Base ` G ) $.
    cayleylem1.p $e |- P = ( +g ` G ) $.
    cayleylem1.u $e |- U = ( 0g ` G ) $.
    cayleylem1.h $e |- H = ( SymGrp ` X ) $.
    cayleylem1.s $e |- S = ( Base ` H ) $.
    cayleylem1.f $e |- F = ( g e. X |-> ( a e. X |-> ( g P a ) ) ) $.
    $( Lemma for ~ cayley . $)
    cayleylem1 $p |- ( G e. Grp -> F e. ( G GrpHom H ) ) $=
      ( vx vy wcel co cv cgrp cghm eqidd cmpt2 cga cvv eqid gaid2 cbs fvex cmpt
      eqeltri oveq12 ovex ovmpt2a mpteq2dva mpteq2ia galactghm sylancl eqeltrrd
      cfv eqtr4i ) FUARZEEFGUBSZVCEUCVCPQHHPTZQTZASZUDZFHUESRHUFREVDRPQAVHFHJKV
      HUGZUHHFUIVAUFJFUIUJULDIEFGVHUFHHJMEDHIHDTZITZASZUKZUKDHIHVJVKVHSZUKZUKOD
      HVOVMVJHRIHVNVLPQVJVKHHVGVLVHVEVJVFVKAUMVIVJVKAUNUOUPUQVBURUSUT $.
      $( [13-Jan-2015] $) $( [3-Mar-2008] $)

    $( Lemma for ~ cayley . $)
    cayleylem2 $p |- ( G e. Grp -> F : X -1-1-> S ) $=
      ( vx wcel cfv wceq cvv cgrp wf1 cv c0g wi wral wa fveq1 co grpidcl adantr
      simpr grplactval syl2anc grprid eqtrd cid cres fvex eqeltri symgid fveq1i
      cbs ax-mp fvresi syl5eqr eqeq12d syl5ib ralrimiva cghm cayleylem1 symggrp
      syl wb eqid ghmf1 sylancl mpbird ) FUAQZHBEUBZPUCZERZGUDRZSZWACSZUEZPHUFZ
      VSWFPHWDCWBRZCWCRZSVSWAHQZUGZWECWBWCUHWKWHWAWICWKWHWACAUIZWAWKWJCHQZWHWLS
      VSWJULVSWMWJHFCJLUJUKZWACADEFHIOJUMUNHAFCWAJKLUOUPWKWICUQHURZRZCCWOWCHTQZ
      WOWCSHFVCRTJFVCUSUTZHGTMVAVDVBWKWMWPCSWNHCVEVMVFVGVHVIVSEFGVJUIQGUAQZVTWG
      VNABCDEFGHIJKLMNOVKWQWSWRHGTMVLVDPFGWCEUAHBCJNLWCVOVPVQVR $.
      $( [13-Jan-2015] $) $( [3-Mar-2008] $)
  $}

  ${
    $d a f g s G $.  $d f g s H $.  $d a g P $.  $d a f g s X $.
    cayley.x $e |- X = ( Base ` G ) $.
    cayley.h $e |- H = ( SymGrp ` X ) $.
    ${
      cayley.p $e |- P = ( +g ` G ) $.
      cayley.f $e |- F = ( g e. X |-> ( a e. X |-> ( g P a ) ) ) $.
      cayley.s $e |- S = ran F $.
      $( Cayley's Theorem (constructive version): given group ` G ` , ` F ` is
         an isomorphism between ` G ` and the subgroup ` S ` of the symmetry
         group ` H ` on the underlying set ` X ` of ` G ` .  (Contributed by
         Paul Chapman, 3-Mar-2008.)  (The proof was shortened by Mario
         Carneiro, 13-Jan-2015.) $)
      cayley $p |- ( G e. Grp -> ( S e. ( SubGrp ` H ) /\
        F e. ( G GrpHom ( H |`s S ) ) /\ F : X -1-1-onto-> S ) ) $=
        ( cgrp wcel cfv co cbs eqid cvv csubg cghm wf1o crn c0g cayleylem1 fvex
        cress eqeltri symggrp ax-mp ghmrn sylancl syl5eqel wb eqimss2i resghm2b
        wss a1i syl2anc mpbid wf1 cayleylem2 f1f1orn wceq f1oeq3 sylibr 3jca
        syl ) ENOZBFUAPZOZDEFBUHQZUBQOZGBDUCZVJBDUDZVKMVJDEFUBQOZFNOZVPVKOAFRPZ
        EUEPZCDEFGHIKVTSZJVSSZLUFZGTOVRGERPTIERUGUIGFTJUJUKEFDNULUMUNZVJVQVNWCV
        JVLVPBURZVQVNUOWDWEVJBVPMUPUSEFVMDBVMSUQUTVAVJGVPDUCZVOVJGVSDVBWFAVSVTC
        DEFGHIKWAJWBLVCGVSDVDVIBVPVEVOWFUOMBVPGDVFUKVGVH $.
        $( [13-Jan-2015] $) $( [3-Mar-2008] $)
    $}

    $( Cayley's Theorem (existence version): every group ` G ` is isomorphic to
       a subgroup of the symmetry group on the underlying set of ` G ` .  (For
       any group ` G ` there exists an isomorphism ` f ` between ` G ` and a
       subgroup ` h ` of the symmetry group on the underlying set of ` G ` .)
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    cayleyth $p |- ( G e. Grp -> E. s e. ( SubGrp ` H )
      E. f e. ( G GrpHom ( H |`s s ) ) f : X -1-1-onto-> s ) $=
      ( vg va wcel cv cfv co cmpt wf1o cress cghm wrex eqid rcla4ev cgrp cplusg
      csubg cayley simp1d simp2d simp3d f1oeq1 syl2anc wceq oveq2 oveq2d f1oeq3
      crn rexeqbidv ) BUAJZHDIDHKIKBUBLZMNNZUNZCUCLZJZDUSAKZOZABCUSPMZQMZRZDEKZ
      VBOZABCVGPMZQMZRZEUTRUPVAURVEJZDUSUROZUQUSHURBCDIFGUQSURSUSSUDZUEUPVLVMVF
      UPVAVLVMVNUFUPVAVLVMVNUGVCVMAURVEDUSVBURUHTUIVKVFEUSUTVGUSUJZVHVCAVJVEVOV
      IVDBQVGUSCPUKULVGUSDVBUMUOTUI $.
      $( [13-Jan-2015] $) $( [3-Mar-2008] $)
  $}

  ${
    $d g h x y A $.  $d g h x y B $.  $d f h k u v w G $.  $d f h k u v w R $.
    $d f g h k u v w x y M $.  $d f g h k x y X $.  $d f h k u v w x y Y $.
    $d h k u v w V $.
    gaorb.1 $e |- R = { <. x , y >. |
      ( { x , y } C_ Y /\ E. g e. X ( g M x ) = y ) } $.
    $( The orbit equivalence relation puts two points in the group action in
       the same equivalence class iff there is a group element that takes one
       element to the other.  (Contributed by Mario Carneiro, 14-Jan-2015.) $)
    gaorb $p |- ( B e. V -> ( A R B <->
      ( A e. Y /\ B e. Y /\ E. h e. X ( h M A ) = B ) ) ) $=
      ( wcel cvv cv co wceq wrex wi wa wbr w3a cpr wss relopabi brrelexi a1i wb
      elex 3ad2ant1 vex eleq1 bi2anan9 syl5bbr oveq2 eqeq12 sylan rexbidv oveq1
      prss eqeq1d cbvrexv syl6bb anbi12d df-3an syl6bbr brabga expcom pm5.21ndd
      ) DIMZCNMZCDEUAZCKMZDKMZGOZCHPZDQZGJRZUBZVLVKSVJCDEAOZBOZUCKUDZFOZVTHPZWA
      QZFJRZTZABELUEUFUGVSVKSVJVMVNVKVRCKUIUJUGVKVJVLVSUHWGVSABCDENIVTCQZWADQZT
      ZWGVMVNTZVRTVSWJWBWKWFVRWBVTKMZWAKMZTWJWKVTWAKAUKBUKUTWHWLVMWIWMVNVTCKULW
      ADKULUMUNWJWFWCCHPZDQZFJRVRWJWEWOFJWHWDWNQWIWEWOUHVTCWCHUOWDWNWADUPUQURWO
      VQFGJWCVOQWNVPDWCVOCHUSVAVBVCVDVMVNVRVEVFLVGVHVI $.
      $( [14-Jan-2015] $)

    gaorber.2 $e |- X = ( Base ` G ) $.
    $( The orbit equivalence relation is an equivalence relation on the target
       set of the group action. $)
    gaorber $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V ) ->
        ( Er R /\ dom R = Y ) ) $=
      ( vk vh co wcel wa cv wceq wrex cvv vu vv vw vf cga wbr simpr gaorb ax-mp
      w3a wb vex sylib simp2d simp1d simp3d cminusg simpll adantr eqid syl13anc
      cfv gacan wi gagrp ad2antrr grpinvcl sylan oveq1 eqeq1d rcla4ev ex sylbid
      cgrp syl rexlimdva syl3anbrc adantrr simprr reeanv cplusg simprlr simprll
      mpd ad3antrrr grpcl syl3anc simprrl oveq2d simprrr 3eqtrd expr rexlimdvva
      gaass syl2anc syl5bir mp2and c0g 3ad2ant1 grpidcl gagrpid 3expia pm4.71rd
      df-3an anidm anbi1i ancom 3bitri syl6bbr iser2d ) FEIUENOZIGOZPZUAUBUCICX
      MUAQZUBQZCUFZPZXOIOZXNIOZLQZXOFNZXNRZLHSZXOXNCUFZXQXSXRMQZXNFNZXORZMHSZXQ
      XPXSXRYHUJZXMXPUGXOTOXPYIUKUBULABXNXOCDMFTHIJUHUIUMZUNZXQXSXRYHYJUOZXQYHY
      CXQXSXRYHYJUPZXQYGYCMHXQYEHOZPZYGYEEUQVBZVBZXOFNZXNRZYCYOXMYNXSXRYGYSUKXM
      XPYNURXQYNUGXQXSYNYLUSXQXRYNYKUSYEXNXOEFYPGHIKYPUTZVCVAYOYQHOZYSYCVDXQEVN
      OZYNUUAXKUUBXLXPEFIVEZVFHEYPYEKYTVGVHUUAYSYCYBYSLYQHXTYQRYAYRXNXTYQXOFVIV
      JVKVLVOVMVPWDXNTOZYDXRXSYCUJUKUAULZABXOXNCDLFTHIJUHUIVQXMXPXOUCQZCUFZPZPZ
      XSUUFIOZUDQZXNFNZUUFRZUDHSZXNUUFCUFZXMXPXSUUGYLVRZUUIXRUUJYAUUFRZLHSZUUIU
      UGXRUUJUURUJZXMXPUUGVSUUFTOZUUGUUSUKUCULZABXOUUFCDLFTHIJUHUIUMZUNUUIYHUUR
      UUNXMXPYHUUGYMVRUUIXRUUJUURUVBUPYHUURPYGUUQPZLHSMHSUUIUUNYGUUQMLHHVTUUIUV
      CUUNMLHHUUIYNXTHOZPZUVCUUNUUIUVEUVCPZPZXTYEEWAVBZNZHOZUVIXNFNZUUFRZUUNUVG
      UUBUVDYNUVJXKUUBXLUUHUVFUUCWEUUIYNUVDUVCWBZUUIYNUVDUVCWCZHUVHEXTYEKUVHUTZ
      WFWGUVGUVKXTYFFNZYAUUFUVGXMUVDYNXSUVKUVPRXMUUHUVFURUVMUVNUUIXSUVFUUPUSXTY
      EXNUVHEFGHIKUVOWNVAUVGYFXOXTFUUIUVEYGUUQWHWIUUIUVEYGUUQWJWKUUMUVLUDUVIHUU
      KUVIRUULUVKUUFUUKUVIXNFVIVJVKWOWLWMWPWQUUTUUOXSUUJUUNUJUKUVAABXNUUFCDUDFT
      HIJUHUIVQXMXSXSXSYFXNRZMHSZUJZXNXNCUFZXMXSUVRXSPZUVSXMXSUVRXKXLXSUVRXKXLX
      SUJZEWRVBZHOZUWCXNFNZXNRZUVRUWBUUBUWDXKXLUUBXSUUCWSHEUWCKUWCUTZWTVOXNUWCE
      FGIUWGXAUVQUWFMUWCHYEUWCRYFUWEXNYEUWCXNFVIVJVKWOXBXCUVSXSXSPZUVRPXSUVRPUW
      AXSXSUVRXDUWHXSUVRXSXEXFXSUVRXGXHXIUUDUVTUVSUKUUEABXNXNCDMFTHIJUHUIXIXJ
      $.
      $( [13-Jan-2015] $) $( [11-Aug-2009] $)
  $}

  ${
    $d a b g h s u w x y z A $.  $d a b h w z F $.  $d a b g h s u w x y z G $.
    $d g s u x B $.  $d x y H $.  $d a b g h s u w x y z M $.  $d a h w z O $.
    $d a b g h s w x y z R $.  $d g s x S $.  $d a b g h s u w x y z X $.
    $d u C $.  $d a b g h s w x y z V $.  $d a b g h s w x y z Y $.
    gasta.1 $e |- X = ( Base ` G ) $.
    gasta.2 $e |- H = { u e. X | ( u M A ) = A } $.
    $( The stabilizer subgroup in a group action. $)
    gastacl $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ A e. Y ) ->
      H e. ( SubGrp ` G ) ) $=
      ( vx co wcel cfv wa wceq syl oveq1 eqeq1d elrab2 vy cga w3a csubg wss wne
      c0 cv cplusg wral cminusg crab ssrab2 eqsstri a1i c0g cgrp gagrp 3ad2ant1
      eqid grpidcl gagrpid sylanbrc ne0i simpl1 simpr sylib simpld simprr grpcl
      adantrr syl3anc simpl2 simpl3 gaass syl23anc simprd oveq2d 3eqtrd anassrs
      ralrimiva grpinvcl syl2anc wb gacan mpbid jca issubg2 mpbir3and ) ECHUBLM
      ZHFMZBHMZUCZDCUDNMZDGUEZDUGUFZKUHZUAUHZCUINZLZDMZUADUJZWQCUKNZNZDMZOZKDUJ
      ZWOWMDAUHZBELZBPZAGULGJXJAGUMUNUOWMCUPNZDMZWPWMXKGMZXKBELZBPZXLWMCUQMZXMW
      JWKXPWLCEHURZUSZGCXKIXKUTZVAQBXKCEFHXSVBXJXOAXKGDXHXKPXIXNBXHXKBERSJTVCDX
      KVDQWMXFKDWMWQDMZOZXBXEYAXAUADWMXTWRDMZXAWMXTYBOZOZWTGMZWTBELZBPZXAYDXPWQ
      GMZWRGMZYEYDWJXPWJWKWLYCVEZXQQWMXTYHYBYAYHWQBELZBPZYAXTYHYLOWMXTVFXJYLAWQ
      GDXHWQPXIYKBXHWQBERSJTVGZVHZVKZYDYIWRBELZBPZYDYBYIYQOWMXTYBVIXJYQAWRGDXHW
      RPXIYPBXHWRBERSJTVGZVHZGWSCWQWRIWSUTZVJVLYDYFWQYPELZYKBYDWJWKYHYIWLYFUUAP
      YJWJWKWLYCVMYOYSWMXTWLYBWJWKWLXTVNZVKWQWRBWSCEFGHIYTVOVPYDYPBWQEYDYIYQYRV
      QVRWMXTYLYBYAYHYLYMVQZVKVSXJYGAWTGDXHWTPXIYFBXHWTBERSJTVCVTWAYAXDGMZXDBEL
      ZBPZXEYAXPYHUUDYAWJXPWJWKWLXTVEZXQQYNGCXCWQIXCUTZWBWCYAYLUUFUUCYAWJWKYHWL
      WLYLUUFWDUUGWJWKWLXTVMYNUUBUUBWQBBCEXCFGHIUUHWEVPWFXJUUFAXDGDXHXDPXIUUEBX
      HXDBERSJTVCWGWAWMXPWNWOWPXGUCWDXRKUAGWSDCXCIYTUUHWHQWI $.
      $( [15-Jan-2015] $)

    orbsta.r $e |- R = ( G ~QG H ) $.
    $( Write the coset relation for the stabilizer subgroup. $)
    gastacos $p |- ( ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ A e. Y ) /\
      ( B e. X /\ C e. X ) ) -> ( B R C <-> ( B M A ) = ( C M A ) ) ) $=
      ( co wcel wa cfv wceq wb cga w3a wbr cminusg cplusg cgrp wss csubg adantr
      gastacl subgrcl syl subgss simprr eqid eqgval syl3anc df-3an syl6bb simpr
      biantrurd simpl1 simpl2 simprl grpinvcl syl2anc simpl3 gaass eqeq1d grpcl
      syl23anc cv oveq1 elrab2 baib cxp wf gaf fovrn gacan 3bitr4d 3bitr2d ) HF
      KUAOPZKIPZBKPZUBZCJPZDJPZQZQZCDEUCZWICFUDRZRZDFUERZOZGPZQZWPCBHODBHOZSZWJ
      WKWGWHWPUBZWQWJFUFPZGJUGZWHWKWTTWJGFUHRPZXAWFXCWIABFGHIJKLMUJUIZGFUKULZWJ
      XCXBXDJGFLUMULWFWGWHUNZCDWNEGFWLUFJJLWLUOZWNUOZNUPUQWGWHWPURUSWJWIWPWFWIU
      TVAWJWOBHOZBSZWMWRHOZBSZWPWSWJXIXKBWJWCWDWMJPZWHWEXIXKSWCWDWEWIVBZWCWDWEW
      IVCZWJXAWGXMXEWFWGWHVDZJFWLCLXGVEVFZXFWCWDWEWIVGZWMDBWNFHIJKLXHVHVKVIWJWO
      JPZWPXJTWJXAXMWHXSXEXQXFJWNFWMDLXHVJUQWPXSXJAVLZBHOZBSXJAWOJGXTWOSYAXIBXT
      WOBHVMVIMVNVOULWJWCWDWGWEWRKPZWSXLTXNXOXPXRWJJKVPKHVQZWHWEYBWJWCWDYCXNXOF
      HIJKLVRVFXFXRDBKJKHVSUQCBWRFHWLIJKLXGVTVKWAWB $.
      $( [15-Jan-2015] $)

    $( Existence and uniqueness for the function of ~ orbsta . $)
    orbstaeu $p |- ( ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ A e. Y ) /\
      S e. ( X /. R ) ) -> E! x E. g e. S x = ( g M A ) ) $=
      ( vh wceq wrex wcel wa vs vy cv cec weu cga w3a cqs eqid rexeq eubidv wex
      co wi wal c0 wne wral cdm wer csubg cfv gastacl syl simprd eleq2d biimpar
      eqger ecdmn0 sylib ovex isseti r19.2z sylancl rexcom4 reeanv wbr vex elec
      rgenw anbi12i cvv simpld adantr a1i ersymb anbi1d ertr sylbid imp cminusg
      simpr wb cplusg subgrcl subgss eqgval mp3an3 syl2anc biimpa simp1d simp2d
      cgrp wss jca gastacos syldan adantlr eqeq12 syl5ibrcom sylan2b rexlimdvva
      mpbid syl5bir alrimivv eqeq1 rexbidv oveq1 eqeq2d cbvrexv syl6bb sylanbrc
      eu4 ectocld ) AUCZFUCZCIUMZQZFUAUCZDUDZRZAUEZYHFERZAUEIGLUFUMSLJSCLSUGZUA
      EKDKDUHZYOUIYJEQYKYMAYHFYJEUJUKYNYIKSZTZYKAULZYKUBUCZPUCZCIUMZQZPYJRZTZYE
      YSQZUNZUBUOAUOYLYQYHAULZFYJRZYRYQYJUPUQZUUGFYJURUUHYQYIDUSZSZUUIYNUUKYPYN
      UUJKYIYNDUTZUUJKQZYNHGVAVBSZUULUUMTBCGHIJKLMNVCZDGKHMOVHVDZVEVFVGYIDVIVJU
      UGFYJAYGYFCIVKVLVTUUGFYJVMVNYHFAYJVOVJYQUUFAUBUUDYHUUBTZPYJRFYJRYQUUEYHUU
      BFPYJYJVPYQUUQUUEFPYJYJYFYJSZYTYJSZTYQYIYFDVQZYIYTDVQZTZUUQUUEUNUURUUTUUS
      UVAYFYIDFVRZUAVRZVSYTYIDPVRZUVDVSWAYQUVBTUUEUUQYGUUAQZYQUVBYFYTDVQZUVFYQU
      VBUVGYQUVBYFYIDVQZUVATUVGYQUUTUVHUVAYQYIYFDKWBYNUULYPYNUULUUMUUPWCWDZYNYP
      WLZYFWBSYQUVCWEZWFWGYQYFYIYTDWBKWBUVIUVKUVJYTWBSZYQUVEWEWHWIWJYNUVGUVFYPY
      NUVGTZUVGUVFYNUVGWLYNUVGYFKSZYTKSZTUVGUVFWMUVMUVNUVOUVMUVNUVOYFGWKVBZVBYT
      GWNVBZUMHSZYNUVGUVNUVOUVRUGZYNUUNUVGUVSWMZUUOUUNGXCSZHKXDZUVTHGWOKHGMWPUW
      AUWBUVLUVTUVEYFYTUVQDHGUVPXCWBKMUVPUIUVQUIOWQWRWSVDWTZXAUVMUVNUVOUVRUWCXB
      XEBCYFYTDGHIJKLMNOXFXGXMXHXGYEYGYSUUAXIXJXKXLXNXOYKUUCAUBUUEYKYSYGQZFYJRU
      UCUUEYHUWDFYJYEYSYGXPXQUWDUUBFPYJYFYTQYGUUAYSYFYTCIXRXSXTYAYCYBYD $.
      $( [15-Jan-2015] $)

    orbsta.f $e |- F = ( s e. ( X /. R ) |->
      ( iota x E. g e. s x = ( g M A ) ) ) $.
    $( Value of the function at a given equivalence class element. $)
    orbstaval $p |- ( ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ A e. Y ) /\
      B e. X ) -> ( F ` [ B ] R ) = ( B M A ) ) $=
      ( wcel wceq cga co w3a wa cec cfv cv wrex cio cqs cqg cvv eqeltri ecelqsi
      adantl rexeq iotabidv iotaex fvmpt syl wbr wer csubg gastacl adantr eqger
      ovex simpld simprd simpr erref wb elecg syl2anc mpbird eqidd oveq1 eqeq2d
      cdm rcla4ev a1i weu orbstaeu sylan2 eqeq1 rexbidv iota2d mpbid eqtrd ) JH
      MUAUBSMKSCMSUCZDLSZUDZDEUEZGUFZAUGZFUGZCJUBZTZFWMUHZAUIZDCJUBZWLWMLEUJZSZ
      WNWTTWKXCWJLDEEHIUKUBULQHIUKVGUMUNZUONWMWRFNUGZUHZAUIWTXBGXEWMTXFWSAWRFXE
      WMUPUQRWSAURUSUTWLXAWQTZFWMUHZWTXATWLDWMSZXAXATZXHWLXIDDEVAZWLDELWLEVBZEV
      SLTZWLIHVCUFSZXLXMUDWJXNWKBCHIJKLMOPVDVEEHLIOQVFUTZVHWLXLXMXOVIWJWKVJZVKW
      LWKWKXIXKVLXPXPDDELLVMVNVOWLXAVPXGXJFDWMWPDTWQXAXAWPDCJVQVRVTVNWLWSXHAXAU
      LXAULSWLDCJVGWAWKWJXCWSAWBXDABCEWMFHIJKLMOPQWCWDWOXATZWSXHVLWLXQWRXGFWMWO
      XAWQWEWFUOWGWHWI $.
      $( [15-Jan-2015] $)

    orbsta.o $e |- O = { <. x , y >. |
      ( { x , y } C_ Y /\ E. g e. X ( g M x ) = y ) } $.
    $( The Orbit-Stabilizer theorem.  The mapping ` F ` is a bijection from the
       cosets of the stabilizer subgroup of ` A ` to the orbit of ` A ` .
       (Contributed by Mario Carneiro, 15-Jan-2015.) $)
    orbsta $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ A e. Y ) ->
      F : ( X /. R ) -1-1-onto-> [ A ] O ) $=
      ( va vb vz vh vw cga co wcel w3a cqs cec wf1 wfo wf1o wf cv cfv wceq wral
      wi wrex cio cvv wa iotaex a1i cmpt eqid fveq2 eleq1d orbstaval wbr simpl3
      cxp gaf 3adant3 adantr simpr fovrn syl3anc eqeq1d rcla4ev sylancl wb ovex
      oveq1 gaorb ax-mp syl3anbrc sylancr mpbird eqeltrd ectocld fmpt2d imbi12d
      elecg eqeq1 ralbidv eqeq2d eqeq2 adantrr adantrl eqeq12d gastacos wer cdm
      csubg gastacl syl simpld simprd simprl simprr erth 3bitr2d biimpd anassrs
      eqger ralrimiva dff13 sylanbrc vex simp3 syl6bb biimpa simp3d cqg eqeltri
      ecelqsi adantl eqcomd syl2anc syl5ibcom rexlimdva imp syldan dffo3 df-f1o
      rexbidv ) JHNUFUGUHZNLUHZDNUHZUIZMEUJZDKUKZGULZUUDUUEGUMZUUDUUEGUNUUCUUDU
      UEGUOZUAUPZGUQZUBUPZGUQZURZUUIUUKURZUTZUBUUDUSZUAUUDUSUUFUUCOUAUUDAUPFUPD
      JUGURFOUPZVAZAVBZUUEGVCUUSVCUHUUCUUQUUDUHVDUURAVEVFGOUUDUUSVGURUUCSVFUCUP
      ZEUKZGUQZUUEUHUUJUUEUHUUCUCUUIMEUUDUUDVHZUVAUUIURZUVBUUJUUEUVAUUIGVIZVJUU
      CUUTMUHZVDZUVBUUTDJUGZUUEACDUUTEFGHIJLMNOPQRSVKZUVGUVHUUEUHZDUVHKVLZUVGUU
      BUVHNUHZUDUPZDJUGZUVHURZUDMVAZUVKYTUUAUUBUVFVMZUVGMNVNNJUOZUVFUUBUVLUUCUV
      RUVFYTUUAUVRUUBHJLMNPVOVPVQUUCUVFVRZUVQUUTDNMNJVSVTUVGUVFUVHUVHURZUVPUVSU
      VHVHUVOUVTUDUUTMUVMUUTURUVNUVHUVHUVMUUTDJWFWAWBWCUVHVCUHZUVKUUBUVLUVPUIWD
      UUTDJWEZABDUVHKFUDJVCMNTWGWHWIUVGUWAUUBUVJUVKWDUWBUVQUVHDKVCNWPWJWKWLWMWN
      ZUUCUUPUAUUDUVBUULURZUVAUUKURZUTZUBUUDUSUUPUUCUCUUIMEUUDUVCUVDUWFUUOUBUUD
      UVDUWDUUMUWEUUNUVDUVBUUJUULUVEWAUVAUUIUUKWQWOWRUVGUWFUBUUDUVBUEUPZEUKZGUQ
      ZURZUVAUWHURZUTZUWFUVGUEUUKMEUUDUVCUWHUUKURZUWJUWDUWKUWEUWMUWIUULUVBUWHUU
      KGVIWSUWHUUKUVAWTWOUUCUVFUWGMUHZUWLUUCUVFUWNVDZVDZUWJUWKUWPUWJUVHUWGDJUGZ
      URUUTUWGEVLUWKUWPUVBUVHUWIUWQUUCUVFUVBUVHURUWNUVIXAUUCUWNUWIUWQURUVFACDUW
      GEFGHIJLMNOPQRSVKZXBXCCDUUTUWGEHIJLMNPQRXDUWPUUTUWGEMMUWPEXEZEXFMURZUUCUW
      SUWTVDZUWOUUCIHXGUQUHUXACDHIJLMNPQXHEHMIPRXRXIVQZXJUWPUWSUWTUXBXKUUCUVFUW
      NXLUUCUVFUWNXMXNXOXPXQWMXSWMXSUAUBUUDUUEGXTYAUUCUUHUVMUUTGUQZURZUCUUDVAZU
      DUUEUSUUGUWCUUCUXEUDUUEUUCUVMUUEUHZUWQUVMURZUEMVAZUXEUUCUXFVDUUBUVMNUHZUX
      HUUCUXFUUBUXIUXHUIZUUCUXFDUVMKVLZUXJUUCUVMVCUHZUUBUXFUXKWDUDYBZYTUUAUUBYC
      UVMDKVCNWPWJUXLUXKUXJWDUXMABDUVMKFUEJVCMNTWGWHYDYEYFUUCUXHUXEUUCUXGUXEUEM
      UUCUWNVDZUWQUXCURZUCUUDVAZUXGUXEUXNUWHUUDUHZUWQUWIURZUXPUWNUXQUUCMUWGEEHI
      YGUGVCRHIYGWEYHYIYJUXNUWIUWQUWRYKUXOUXRUCUWHUUDUUTUWHURUXCUWIUWQUUTUWHGVI
      WSWBYLUXGUXOUXDUCUUDUWQUVMUXCWQYSYMYNYOYPXSUCUDUUDUUEGYQYAUUDUUEGYRYA $.
      $( [15-Jan-2015] $)
  $}

  ${
    $d g u x y z A $.  $d g u x y z G $.  $d g u x y z M $.  $d g u x y z X $.
    $d x y H $.  $d g x y z R $.  $d g x y z V $.  $d g x y z Y $.
    orbsta2.x $e |- X = ( Base ` G ) $.
    orbsta2.h $e |- H = { u e. X | ( u M A ) = A } $.
    orbsta2.r $e |- R = ( G ~QG H ) $.
    orbsta2.o $e |- O = { <. x , y >. |
      ( { x , y } C_ Y /\ E. g e. X ( g M x ) = y ) } $.
    $( Relation between the size of the orbit and the size of the stabilizer of
       a point in a finite group action.  (Contributed by Mario Carneiro,
       16-Jan-2015.) $)
    orbsta2 $p |- ( ( ( M e. ( G GrpAct Y ) /\ Y e. V /\ A e. Y ) /\
      X e. Fin ) -> ( # ` X ) = ( ( # ` [ A ] O ) x. ( # ` H ) ) ) $=
      ( co wcel cvv vz cga w3a cfn wa chash cfv cqs cmul cec csubg adantr simpr
      gastacl lagsubg2 wceq cen wbr wrex cio cmpt wf1o eqid orbsta fvex eqeltri
      cv cbs qsex f1oen syl cpw wss pwfi sylib wer cdm eqger simpld simprd ovex
      wb cqg a1i qsss ssfi syl2anc crn cxp cpr relopabi relssdmrn ax-mp gaorber
      wrel 3adant3 simpl2 eqeltrd eqeltrrd xpexg ssexg sylancr ecexg enfi mpbid
      erdmrn hashen mpbird oveq1d eqtrd ) IGMUBRSZMKSZDMSZUCZLUDSZUEZLUFUGLEUHZ
      UFUGZHUFUGZUIRDJUJZUFUGZXSUIRXPEGLHNPXNHGUKUGSZXOCDGHIKLMNOUNULZXNXOUMZUO
      XPXRYAXSUIXPXRYAUPZXQXTUQURZXPXQXTUAXQAVGZFVGZDIRUPFUAVGUSAUTVAZVBZYFXNYJ
      XOABCDEFYIGHIJKLMUANOPYIVCQVDULXQXTYILELGVHUGTNGVHVEVFVIVJVKZXPXQUDSZXTUD
      SZYEYFWBXPLVLZUDSZXQYNVMYLXPXOYOYDLVNVOXPLETXPEVPZEVQLUPZXPYBYPYQUEYCEGLH
      NPVRVKZVSXPYPYQYRVTETSXPEGHWCRTPGHWCWAVFWDWEYNXQWFWGZXPYLYMYSXPXTTSZYFYLY
      MWBXPJTSZYTXPJJVQZJWHZWIZVMZUUDTSZUUAJWOUUEYGBVGZWJMVMYHYGIRUUGUPFLUSUEAB
      JQWKJWLWMXPUUBKSUUCKSUUFXPUUBMKXPJVPZUUBMUPZXNUUHUUIUEZXOXKXLUUJXMABJFGIK
      LMQNWNWPULZVTXKXLXMXOWQWRZXPUUBUUCKXPUUHUUBUUCUPXPUUHUUIUUKVSJXFVKUULWSUU
      BUUCKKWTWGJUUDTXAXBDTJXCVKYKXQXTTXDWGXEXQXTXGWGXHXIXJ $.
      $( [16-Jan-2015] $)
  $}


