$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Symmetry groups and Cayley's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SymGrp $.

  $( Extend class notation to include the class of symmetry groups. $)
  csymg $a class SymGrp $.

  ${
    $d b f g h x $.
    $( Define the symmetry group on set ` x ` .  We represent the group as the
       set of 1-1-onto functions from ` x ` to itself under function
       composition.  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    df-symg $a |- SymGrp = ( x e. _V |-> [_ { h | h : x -1-1-onto-> x } / b ]_
      { <. ( Base ` ndx ) , b >. ,
        <. ( +g ` ndx ) , ( f e. b , g e. b |-> ( f o. g ) ) >. } ) $.
  $}

  ${
    $d a b f g x A $.  $d a b B $.  $d a b P $.
    symgval.1 $e |- G = ( SymGrp ` A ) $.
    symgval.2 $e |- B = { x | x : A -1-1-onto-> A } $.
    symgval.3 $e |- P = ( f e. B , g e. B |-> ( f o. g ) ) $.
    $( The value of the symmetry group function at ` A ` .  (Contributed by
       Paul Chapman, 25-Feb-2008.)  (Revised by Mario Carneiro,
       12-Jan-2015.) $)
    symgval $p |- ( A e. V -> G = { <. ( Base ` ndx ) , B >. ,
        <. ( +g ` ndx ) , P >. } ) $=
      ( va vb wcel csymg cfv cop cvv wceq cv cnx cbs cplusg cpr elex wf1o cmpt2
      cab ccom csb cmap co ovex wf f1of vex elmap sylibr abssi ssexi wa f1oeq23
      a1i id wb anidms abbidv syl6eqr sylan9eqr opeq2d eqidd mpt2eq123dv csbied
      preq12d df-symg prex fvmpt syl syl5eq ) BHNZGBOPZUAUBPZCQZUAUCPZDQZUDZIVT
      BRNWAWFSBHUELBMLTZWGATZUFZAUHZWBMTZQZWDEFWKWKETFTUIZUGZQZUDZUJWFROWGBSZMW
      JWPWFRWJRNWQWJWGWGUKULZWGWGUKUMWIAWRWIWGWGWHUNWHWRNWGWGWHUOWGWGWHLUPZWSUQ
      URUSUTVCWQWKWJSZVAZWLWCWOWEXAWKCWBWTWQWKWJCWTVDWQWJBBWHUFZAUHCWQWIXBAWQWI
      XBVEWGBWGBWHVBVFVGJVHVIZVJXAWNDWDXAWNEFCCWMUGDXAEFWKWKWMCCWMXCXCXAWMVKVLK
      VHVJVNVMLEFAMVOWCWEVPVQVRVS $.
      $( [12-Jan-2015] $) $( [25-Feb-2008] $)
  $}

  ${
    $d f g x A $.  $d x F $.  $d x V $.
    symgbas.1 $e |- G = ( SymGrp ` A ) $.
    symgbas.2 $e |- B = ( Base ` G ) $.
    $( The base set of the symmetric group.  (Contributed by Mario Carneiro,
       12-Jan-2015.) $)
    symgbas $p |- B = { x | x : A -1-1-onto-> A } $=
      ( vf vg cbs cfv cv cvv wcel wceq cnx cop cmap wss eqid c0 wf1o cab cplusg
      ccom cmpt2 cpr co wf f1of elmapg anidms syl5ibr abssdv ovex ssexg sylancl
      wb grpbase syl symgval fveq2d eqtr4d cdm f1odm vex dmex syl6eqelr pm2.21d
      con3i ss0 csymg fvprc syl5eq baseid str0 syl6eqr 3eqtr4a pm2.61i eqtr4i
      wn ) CDIJZBBAKZUAZAUBZFBLMZWDWANWEWDOIJZWDPOUCJGHWDWDGKHKUDUEZPUFZIJZWAWE
      WDLMZWDWINWEWDBBQUGZRWKLMWJWEWCAWKWCWBWKMZWEBBWBUHZBBWBUIWEWLWMUQBBWBLLUJ
      UKULUMBBQUNWDWKLUOUPWDWGWHLWHSURUSWEDWHIABWDWGGHDLEWDSWGSUTVAVBWEVTZTTWDW
      ATSWNWDTRWDTNWNWCATWNWCWBTMWCWEWCBWBVCLBBWBVDWBAVEVFVGVIVHUMWDVJUSWNWATIJ
      TWNDTIWNDBVKJTEBVKVLVMVAIWFAAVNVOVPVQVRVS $.
      $( [12-Jan-2015] $)

    $( Two ways of saying a function is a 1-1-onto mapping of A to itself.
       (Contributed by Mario Carneiro, 28-Jan-2015.) $)
    elsymgbas2 $p |- ( F e. V -> ( F e. B <-> F : A -1-1-onto-> A ) ) $=
      ( vx cv wf1o f1oeq1 symgbas elab2g ) AAHIZJAACJHCBEAANCKHABDFGLM $.
      $( [28-Jan-2015] $)

    $( Two ways of saying a function is a 1-1-onto mapping of A to itself.
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    elsymgbas $p |- ( A e. V -> ( F e. B <-> F : A -1-1-onto-> A ) ) $=
      ( wcel cvv wf1o wi elex a1i wf f1of fex expcom syl5 wb elsymgbas2
      pm5.21ndd ) AEHZCIHZCBHZAACJZUDUCKUBCBLMUEAACNZUBUCAACOUFUBUCAAECPQRUCUDU
      ESKUBABCDIFGTMUA $.
      $( [28-Jan-2015] $) $( [25-Feb-2008] $)

    $( The symmetric group on ` n ` objects has cardinality ` n ! ` .
       (Contributed by Mario Carneiro, 22-Jan-2015.) $)
    symghash $p |- ( A e. Fin -> ( # ` B ) = ( ! ` ( # ` A ) ) ) $=
      ( vf cfn wcel chash cfv cv wf1o cab cfa symgbas fveq2i hashfac syl5eq ) A
      GHBIJAAFKLFMZIJAIJNJBSIFABCDEOPAFQR $.
      $( [22-Jan-2015] $)
  $}

  ${
    $d f g x A $.  $d f g B $.  $d f g X $.  $d f g Y $.
    symgplusg.1 $e |- G = ( SymGrp ` A ) $.
    symgplusg.2 $e |- B = ( Base ` G ) $.
    symgplusg.3 $e |- P = ( +g ` G ) $.
    $( The value of the symmetry group function at ` A ` .  (Contributed by
       Paul Chapman, 25-Feb-2008.) $)
    symgplusg $p |- P = ( f e. B , g e. B |-> ( f o. g ) ) $=
      ( vx cvv wcel cv wceq cplusg cfv cbs fveq2d 3eqtr4g c0 ccom cmpt2 cnx cop
      cpr symgbas eqid symgval eqeltri mpt2ex grpplusg ax-mp csymg fvprc syl5eq
      fvex wn plusgid str0 wfn cxp vex coex fnmpt2i baseid xpeq2d syl6eq fneq2d
      xp0 mpbii fn0 sylib eqtr4d pm2.61i ) AKLZCDEBBDMZEMZUAZUBZNVOFOPZUCQPZBUD
      UCOPZVSUDUEZOPZCVSVOFWCOJABVSDEFKGJABFGHUFVSUGZUHRIVSKLVSWDNDEBBVRBFQPZKH
      FQUPUIZWGUJBVSWCKWCUGUKULSVOUQZCTVSWHVTTOPCTWHFTOWHFAUMPTGAUMUNUOZRIOWBJJ
      URUSSWHVSTUTZVSTNWHVSBBVAZUTWJDEBBVRVSWEVPVQDVBEVBVCVDWHWKTVSWHWKBTVATWHB
      TBWHWFTQPBTWHFTQWIRHQWAJJVEUSSVFBVIVGVHVJVSVKVLVMVN $.
      $( [28-Jan-2015] $) $( [25-Feb-2008] $)

    $( The value of the group operation of the symmetry group on ` A ` .
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgov $p |- ( ( X e. B /\ Y e. B ) ->
      ( X P Y ) = ( X o. Y ) ) $=
      ( vf vg wcel ccom cvv co wceq coexg cv coeq1 coeq2 symgplusg ovmpt2g
      mpd3an3 ) EBLFBLEFMZNLEFCOUDPEFBBQJKEFBBJRZKRZMUDCEUFMNUEEUFSUFFETABCJKDG
      HIUAUBUC $.
      $( [28-Jan-2015] $) $( [25-Feb-2008] $)

    $( The group operation of the symmetry group on ` A ` is closed, i.e. a
       magma.  (Contributed by Mario Carneiro, 12-Jan-2015.) $)
    symgcl $p |- ( ( X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( wcel wa co ccom symgov wf1o elsymgbas2 ibi f1oco syl2an cvv syl eqeltrd
      wb coexg mpbird ) EBJZFBJZKZEFCLEFMZBABCDEFGHINUHUIBJZAAUIOZUFAAEOZAAFOZU
      KUGUFULABEDBGHPQUGUMABFDBGHPQAAAEFRSUHUITJUJUKUCEFBBUDABUIDTGHPUAUEUB $.
      $( [28-Jan-2015] $) $( [12-Jan-2015] $)
  $}

  ${
    $d x y z A $.  $d x y z G $.  $d x y z V $.
    symggrp.1 $e |- G = ( SymGrp ` A ) $.
    $( The symmetry group on ` A ` is a group.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    symggrp $p |- ( A e. V -> G e. Grp ) $=
      ( vx vy vz wcel cfv cv eqidd co eqid symgcl ccom wceq symgov syl2anc wf1o
      elsymgbas cbs cplusg ccnv cid cres 3adant1 w3a coass simpr1 simpr2 coeq1d
      wa simpr3 coeq2d 3eqtr4a 3eqtr4d f1oi mpbiri sylan biimpa f1of fcoi2 3syl
      wf eqtrd wi f1ocnv a1i 3imtr4d imp simpr f1ococnv1 syl isgrpd ) ACHZEFGBU
      AIZBUBIZBEJZUCZUDAUEZVOVPKVOVQKVRVPHZFJZVPHZVRWBVQLZVPHZVOAVPVQBVRWBDVPMZ
      VQMZNZUFVOWAWCGJZVPHZUGULZWDWIOZVRWBWIVQLZOZWDWIVQLZVRWMVQLZWKVRWBOZWIOVR
      WBWIOZOWLWNVRWBWIUHWKWDWQWIWKWAWCWDWQPVOWAWCWJUIZVOWAWCWJUJZAVPVQBVRWBDWF
      WGQRUKWKWMWRVRWKWCWJWMWRPWTVOWAWCWJUMZAVPVQBWBWIDWFWGQRUNUOWKWEWJWOWLPWKW
      AWCWEWSWTWHRXAAVPVQBWDWIDWFWGQRWKWAWMVPHZWPWNPWSWKWCWJXBWTXAAVPVQBWBWIDWF
      WGNRAVPVQBVRWMDWFWGQRUPVOVTVPHZAAVTSAUQAVPVTBCDWFTURZVOWAULZVTVRVQLZVTVRO
      ZVRVOXCWAXFXGPXDAVPVQBVTVRDWFWGQUSXEAAVRSZAAVRVDXGVRPVOWAXHAVPVRBCDWFTZUT
      ZAAVRVAAAVRVBVCVEVOWAVSVPHZVOXHAAVSSZWAXKXHXLVFVOAAVRVGVHXIAVPVSBCDWFTVIV
      JZXEVSVRVQLZVSVROZVTXEXKWAXNXOPXMVOWAVKAVPVQBVSVRDWFWGQRXEXHXOVTPXJAAVRVL
      VMVEVN $.
      $( [13-Jan-2015] $) $( [25-Feb-2008] $)

    $( The value of the identity element of the symmetry group on ` A `
       (Contributed by Paul Chapman, 25-Jul-2008.) $)
    symgid $p |- ( A e. V -> ( _I |` A ) = ( 0g ` G ) ) $=
      ( wcel c0g cfv cid cres cplusg wceq ccom wf1o f1oi eqid elsymgbas syl2anc
      co cbs mpbiri symgov wf f1of fcoi1 mp2b syl6eq cgrp wb grpid mpbid eqcomd
      symggrp ) ACEZBFGZHAIZUMUOUOBJGZRZUOKZUNUOKZUMUQUOUOLZUOUMUOBSGZEZVBUQUTK
      UMVBAAUOMZANZAVAUOBCDVAOZPTZVFAVAUPBUOUODVEUPOZUAQVCAAUOUBUTUOKVDAAUOUCAA
      UOUDUEUFUMBUGEVBURUSUHABCDULVFVAUPBUNUOVEVGUNOUIQUJUK $.
      $( [13-Jan-2015] $) $( [25-Feb-2008] $)
  $}

  ${
    $d w x y z G $.  $d w x y z M $.  $d w x y z V $.  $d w x y z X $.
    $d w z F $.  $d w x z H $.  $d w x y z Y $.
    galactghm.x $e |- X = ( Base ` G ) $.
    galactghm.h $e |- H = ( SymGrp ` Y ) $.
    galactghm.f $e |- F = ( x e. X |-> ( y e. Y |-> ( x M y ) ) ) $.
    $( The currying of a group action is a group homomorphism between the group
       ` G ` and the symetry group ` ( SymGrp `` Y ) ` .  (Contributed by FL,
       17-May-2010.)  (The proof was shortened by Mario Carneiro,
       13-Jan-2015.) $)
    galactghm $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V ) ->
      F e. ( G GrpHom H ) ) $=
      ( co wcel wa cfv wceq cmpt cvv syl2anc vz vw cga cgrp cbs wf cv wral cghm
      cplusg gagrp symggrp anim12i wf1o eqid 3expa wb elsymgbas ad2antlr mpbird
      gapm fmptd df-3an gaass sylan2br anassrs mpteq2dva ad2antrr simprl simprr
      w3a grpcl syl3anc mptexg oveq1 mpteq2dv fvmptg ccom adantr ffvelrn symgov
      cxp simpr fovrn oveq2 cbvmptv syl6eq fmptco eqtrd 3eqtr4d ralrimivva fvex
      gaf jca csymg eqeltri isghm ax-mp sylanbrc ) FDIUCMNZIGNZOZDUDNZEUDNZOZHE
      UEPZCUFZUAUGZUBUGZDUJPZMZCPZXHCPZXICPZEUJPZMZQZUBHUHUAHUHZOZCDEUIMNZWTXCX
      AXDDFIUKZIEGKULUMXBXGXRXBAHBIAUGZBUGZFMZRZXFCXBYBHNZOYEXFNZIIYEUNZWTXAYFY
      HBYBYEDFGHIJYEUOVAUPXAYGYHUQWTYFIXFYEEGKXFUOZURUSUTLVBZXBXQUAUBHHXBXHHNZX
      IHNZOZOZBIXKYCFMZRZBIXHXIYCFMZFMZRZXLXPYNBIYOYRXBYMYCINZYOYRQZYMYTOXBYKYL
      YTVKUUAYKYLYTVCXHXIYCXJDFGHIJXJUOZVDVEVFVGYNXKHNZYPSNZXLYPQYNXCYKYLUUCWTX
      CXAYMYAVHXBYKYLVIZXBYKYLVJZHXJDXHXIJUUBVLVMXAUUDWTYMBIYOGVNUSAXKYEYPHSCYB
      XKQBIYDYOYBXKYCFVOVPLVQTYNXPXMXNVRZYSYNXMXFNZXNXFNZXPUUGQYNXGYKUUHXBXGYMY
      JVSZUUEHXFXHCVTTYNXGYLUUIUUJUUFHXFXICVTTIXFXOEXMXNKYIXOUOZWATYNBAIIYQXHYB
      FMZYRXNXMYNYTOHIWBIFUFZYLYTYQINXBUUMYMYTDFGHIJWMVHYNYLYTUUFVSYNYTWCXIYCIH
      IFWDVMYNYLBIYQRZSNZXNUUNQUUFXAUUOWTYMBIYQGVNUSAXIYEUUNHSCYBXIQBIYDYQYBXIY
      CFVOVPLVQTYNXMBIXHYCFMZRZAIUULRYNYKUUQSNZXMUUQQUUEXAUURWTYMBIUUPGVNUSAXHY
      EUUQHSCYBXHQBIYDUUPYBXHYCFVOVPLVQTBAIUUPUULYCYBXHFWEWFWGYBYQXHFWEWHWIWJWK
      WNESNXTXEXSOUQEIWOPSKIWOWLWPUBUAXJXODECSHXFJYIUUBUUKWQWRWS $.
      $( [13-Jan-2015] $) $( [17-May-2010] $)
  $}

  ${
    $d u v x y z F $.  $d u v x y z G $.  $d u v x y z H $.  $d u v x y z V $.
    $d u v z M $.  $d u v x y X $.  $d u v x y z Y $.
    lactghmga.x $e |- X = ( Base ` G ) $.
    lactghmga.h $e |- H = ( SymGrp ` Y ) $.
    lactghmga.f $e |- M = ( x e. X , y e. Y |-> ( ( F ` x ) ` y ) ) $.
    $( The converse of ~ galactghm .  The uncurrying of a homomorphism into
       ` ( SymGrp `` Y ) ` is a group action.  Thus group actions and group
       homomorphisms into a symmetric group are essentially equivalent
       notions.  (Contributed by Mario Carneiro, 15-Jan-2015.) $)
    lactghmga $p |- ( ( F e. ( G GrpHom H ) /\ Y e. V ) ->
      M e. ( G GrpAct Y ) ) $=
      ( co wcel wa cfv wceq fveq2 fveq1d syl2anc vz vu vv cghm cga cgrp cxp c0g
      wf cv cplusg wral ghmgrp1 adantr wf1o cbs symggrp eqid ghmf ffvelrn sylan
      sylan2 wb elsymgbas ad2antlr mpbid f1of syl ralrimiva fmpt2 sylib grpidcl
      cid cres simpr fvex ovmpt2 symgid eqtr4d fvresi adantl 3eqtrd ccom simprr
      ghmid ad2antrr simpllr fvco3 simplll simprl ghmlin syl22anc eqtrd 3eqtr4d
      symgov grpcl syl3anc oveq2d ralrimivva jca w3a isga mpbir3and ) CDEUDMNZI
      GNZOZFDIUEMNZDUFNZHIUGIFUIZDUHPZUAUJZFMZXKQZUBUJZUCUJZDUKPZMZXKFMZXNXOXKF
      MZFMZQZUCHULUBHULZOZUAIULZXDXHXEDECUMUNZXFBUJZAUJZCPZPZINZBIULZAHULXIXFYK
      AHXFYGHNZOZYJBIYMIIYHUIZYFINYJYMIIYHUOZYNYMYHEUPPZNZYOXFHYPCUIZYLYQXEXDEU
      FNZYRIEGKUQZDECUFHYPJYPURZUSVBZHYPYGCUTVAXEYQYOVCXDYLIYPYHEGKUUAVDVEVFIIY
      HVGVHIIYFYHUTVAVIVIABHIYIIFLVJVKXFYCUAIXFXKINZOZXMYBUUDXLXKXJCPZPZXKVMIVN
      ZPZXKUUDXJHNZUUCXLUUFQXFUUIUUCXFXHUUIYEHDXJJXJURZVLVHUNXFUUCVOZABXJXKHIYI
      UUFFYFUUEPYGXJQYFYHUUEYGXJCRSYFXKUUERLXKUUEVPVQTUUDXKUUEUUGUUDUUEEUHPZUUG
      XFUUEUULQZUUCXEXDYSUUMYTDECUFXJUULUUJUULURWEVBUNXEUUGUULQXDUUCIEGKVRVEVSS
      UUCUUHXKQXFIXKVTWAWBUUDYAUBUCHHUUDXNHNZXOHNZOZOZXKXQCPZPZXNXKXOCPZPZFMZXR
      XTUUQXKXNCPZUUTWCZPZUVAUVCPZUUSUVBUUQIIUUTUIZUUCUVEUVFQUUQIIUUTUOZUVGUUQU
      UTYPNZUVHUUQYRUUOUVIXFYRUUCUUPUUBWFZUUDUUNUUOWDZHYPXOCUTTZUUQXEUVIUVHVCXD
      XEUUCUUPWGZIYPUUTEGKUUAVDVHVFIIUUTVGVHZUUDUUCUUPUUKUNZIIXKUVCUUTWHTUUQXKU
      URUVDUUQUURUVCUUTEUKPZMZUVDUUQXDYSUUNUUOUURUVQQXDXEUUCUUPWIUUQXEYSUVMYTVH
      UUDUUNUUOWJZUVKXPUVPDEXNCXOUFHJXPURZUVPURZWKWLUUQUVCYPNZUVIUVQUVDQUUQYRUU
      NUWAUVJUVRHYPXNCUTTUVLIYPUVPEUVCUUTKUUAUVTWOTWMSUUQUUNUVAINZUVBUVFQUVRUUQ
      UVGUUCUWBUVNUVOIIXKUUTUTTABXNUVAHIYIUVFFYFUVCPYGXNQYFYHUVCYGXNCRSYFUVAUVC
      RLUVAUVCVPVQTWNUUQXQHNZUUCXRUUSQUUQXHUUNUUOUWCXFXHUUCUUPYEWFUVRUVKHXPDXNX
      OJUVSWPWQUVOABXQXKHIYIUUSFYFUURPYGXQQYFYHUURYGXQCRSYFXKUURRLXKUURVPVQTUUQ
      XSUVAXNFUUQUUOUUCXSUVAQUVKUVOABXOXKHIYIUVAFYFUUTPYGXOQYFYHUUTYGXOCRSYFXKU
      UTRLXKUUTVPVQTWRWNWSWTVIXEXGXHXIYDXAVCXDUAUBUCXPXJDFGHIJUVSUUJXBWAXC $.
      $( [15-Jan-2015] $)
  $}

  ${
    $d f x B $.  $d f x G $.  $d f x V $.  $d f x X $.
    symgga.g $e |- G = ( SymGrp ` X ) $.
    symgga.b $e |- B = ( Base ` G ) $.
    symgga.f $e |- F = ( f e. B , x e. X |-> ( f ` x ) ) $.
    $( The symmetric group induces a group action on its base set.
       (Contributed by Mario Carneiro, 24-Jan-2015.) $)
    symgga $p |- ( X e. V -> F e. ( G GrpAct X ) ) $=
      ( cid cres cghm co wcel cga cgrp cv cfv cmpt2 symggrp idghm syl wa fvresi
      wceq adantr fveq1d mpt2eq3ia eqtr4i lactghmga mpancom ) KBLZEEMNOZGFOZDEG
      PNOUOEQOUNGEFHUABEIUBUCCAUMEEDFBGIHDCABGARZCRZSZTCABGUPUQUMSZSZTJCABGUTUR
      UQBOZUPGOZUDUPUSUQVAUSUQUFVBBUQUEUGUHUIUJUKUL $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d a g x y G $.  $d g x H $.  $d a g x y P $.  $d a x U $.  $d a g x y X $.
    $d x F $.  $d x S $.
    cayleylem1.x $e |- X = ( Base ` G ) $.
    cayleylem1.p $e |- P = ( +g ` G ) $.
    cayleylem1.u $e |- U = ( 0g ` G ) $.
    cayleylem1.h $e |- H = ( SymGrp ` X ) $.
    cayleylem1.s $e |- S = ( Base ` H ) $.
    cayleylem1.f $e |- F = ( g e. X |-> ( a e. X |-> ( g P a ) ) ) $.
    $( Lemma for ~ cayley . $)
    cayleylem1 $p |- ( G e. Grp -> F e. ( G GrpHom H ) ) $=
      ( vx vy wcel co cv cgrp cghm eqidd cmpt2 cga cvv eqid gaid2 cbs fvex cmpt
      eqeltri oveq12 ovex ovmpt2a mpteq2dva mpteq2ia galactghm sylancl eqeltrrd
      cfv eqtr4i ) FUARZEEFGUBSZVCEUCVCPQHHPTZQTZASZUDZFHUESRHUFREVDRPQAVHFHJKV
      HUGZUHHFUIVAUFJFUIUJULDIEFGVHUFHHJMEDHIHDTZITZASZUKZUKDHIHVJVKVHSZUKZUKOD
      HVOVMVJHRIHVNVLPQVJVKHHVGVLVHVEVJVFVKAUMVIVJVKAUNUOUPUQVBURUSUT $.
      $( [13-Jan-2015] $) $( [3-Mar-2008] $)

    $( Lemma for ~ cayley . $)
    cayleylem2 $p |- ( G e. Grp -> F : X -1-1-> S ) $=
      ( vx wcel cfv wceq cvv cgrp wf1 cv c0g wi wral wa fveq1 co grpidcl adantr
      simpr grplactval syl2anc grprid eqtrd cid cres fvex eqeltri symgid fveq1i
      cbs ax-mp fvresi syl5eqr eqeq12d syl5ib ralrimiva cghm cayleylem1 symggrp
      syl wb eqid ghmf1 sylancl mpbird ) FUAQZHBEUBZPUCZERZGUDRZSZWACSZUEZPHUFZ
      VSWFPHWDCWBRZCWCRZSVSWAHQZUGZWECWBWCUHWKWHWAWICWKWHWACAUIZWAWKWJCHQZWHWLS
      VSWJULVSWMWJHFCJLUJUKZWACADEFHIOJUMUNHAFCWAJKLUOUPWKWICUQHURZRZCCWOWCHTQZ
      WOWCSHFVCRTJFVCUSUTZHGTMVAVDVBWKWMWPCSWNHCVEVMVFVGVHVIVSEFGVJUIQGUAQZVTWG
      VNABCDEFGHIJKLMNOVKWQWSWRHGTMVLVDPFGWCEUAHBCJNLWCVOVPVQVR $.
      $( [13-Jan-2015] $) $( [3-Mar-2008] $)
  $}

  ${
    $d a f g s G $.  $d f g s H $.  $d a g P $.  $d a f g s X $.
    cayley.x $e |- X = ( Base ` G ) $.
    cayley.h $e |- H = ( SymGrp ` X ) $.
    ${
      cayley.p $e |- P = ( +g ` G ) $.
      cayley.f $e |- F = ( g e. X |-> ( a e. X |-> ( g P a ) ) ) $.
      cayley.s $e |- S = ran F $.
      $( Cayley's Theorem (constructive version): given group ` G ` , ` F ` is
         an isomorphism between ` G ` and the subgroup ` S ` of the symmetry
         group ` H ` on the underlying set ` X ` of ` G ` .  (Contributed by
         Paul Chapman, 3-Mar-2008.)  (The proof was shortened by Mario
         Carneiro, 13-Jan-2015.) $)
      cayley $p |- ( G e. Grp -> ( S e. ( SubGrp ` H ) /\
        F e. ( G GrpHom ( H |`s S ) ) /\ F : X -1-1-onto-> S ) ) $=
        ( cgrp wcel cfv co cbs eqid cvv csubg cghm wf1o crn c0g cayleylem1 fvex
        cress eqeltri symggrp ax-mp ghmrn sylancl syl5eqel wb eqimss2i resghm2b
        wss a1i syl2anc mpbid wf1 cayleylem2 f1f1orn wceq f1oeq3 sylibr 3jca
        syl ) ENOZBFUAPZOZDEFBUHQZUBQOZGBDUCZVJBDUDZVKMVJDEFUBQOZFNOZVPVKOAFRPZ
        EUEPZCDEFGHIKVTSZJVSSZLUFZGTOVRGERPTIERUGUIGFTJUJUKEFDNULUMUNZVJVQVNWCV
        JVLVPBURZVQVNUOWDWEVJBVPMUPUSEFVMDBVMSUQUTVAVJGVPDUCZVOVJGVSDVBWFAVSVTC
        DEFGHIKWAJWBLVCGVSDVDVIBVPVEVOWFUOMBVPGDVFUKVGVH $.
        $( [13-Jan-2015] $) $( [3-Mar-2008] $)
    $}

    $( Cayley's Theorem (existence version): every group ` G ` is isomorphic to
       a subgroup of the symmetry group on the underlying set of ` G ` .  (For
       any group ` G ` there exists an isomorphism ` f ` between ` G ` and a
       subgroup ` h ` of the symmetry group on the underlying set of ` G ` .)
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    cayleyth $p |- ( G e. Grp -> E. s e. ( SubGrp ` H )
      E. f e. ( G GrpHom ( H |`s s ) ) f : X -1-1-onto-> s ) $=
      ( vg va wcel cv cfv co cmpt wf1o cress cghm wrex eqid rcla4ev cgrp cplusg
      csubg cayley simp1d simp2d simp3d f1oeq1 syl2anc wceq oveq2 oveq2d f1oeq3
      crn rexeqbidv ) BUAJZHDIDHKIKBUBLZMNNZUNZCUCLZJZDUSAKZOZABCUSPMZQMZRZDEKZ
      VBOZABCVGPMZQMZRZEUTRUPVAURVEJZDUSUROZUQUSHURBCDIFGUQSURSUSSUDZUEUPVLVMVF
      UPVAVLVMVNUFUPVAVLVMVNUGVCVMAURVEDUSVBURUHTUIVKVFEUSUTVGUSUJZVHVCAVJVEVOV
      IVDBQVGUSCPUKULVGUSDVBUMUOTUI $.
      $( [13-Jan-2015] $) $( [3-Mar-2008] $)
  $}


