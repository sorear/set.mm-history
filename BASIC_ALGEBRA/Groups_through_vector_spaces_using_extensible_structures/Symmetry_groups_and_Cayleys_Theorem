$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Symmetry groups and Cayley's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SymGrp $.

  $( Extend class notation to include the class of symmetry groups. $)
  csymg $a class SymGrp $.

  ${
    $d b f g h x $.
    $( Define the symmetry group on set ` x ` .  We represent the group as the
       set of 1-1-onto functions from ` x ` to itself under function
       composition.  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    df-symg $a |- SymGrp = ( x e. _V |-> [_ { h | h : x -1-1-onto-> x } / b ]_
      { <. ( Base ` ndx ) , b >. ,
        <. ( +g ` ndx ) , ( f e. b , g e. b |-> ( f o. g ) ) >. } ) $.
  $}

  ${
    $d a b f g x A $.  $d a b B $.  $d a b P $.
    symgval.1 $e |- G = ( SymGrp ` A ) $.
    symgval.2 $e |- B = { x | x : A -1-1-onto-> A } $.
    symgval.3 $e |- P = ( f e. B , g e. B |-> ( f o. g ) ) $.
    $( The value of the symmetry group function at ` A ` .  (Contributed by
       Paul Chapman, 25-Feb-2008.)  (Revised by Mario Carneiro,
       12-Jan-2015.) $)
    symgval $p |- ( A e. V -> G = { <. ( Base ` ndx ) , B >. ,
        <. ( +g ` ndx ) , P >. } ) $=
      ( va vb wcel csymg cfv cop cvv wceq cv cnx cbs cplusg cpr elex wf1o cmpt2
      cab ccom csb cmap co ovex wf f1of vex elmap sylibr abssi ssexi wa f1oeq23
      a1i id wb anidms abbidv syl6eqr sylan9eqr opeq2d eqidd mpt2eq123dv csbied
      preq12d df-symg prex fvmpt syl syl5eq ) BHNZGBOPZUAUBPZCQZUAUCPZDQZUDZIVT
      BRNWAWFSBHUELBMLTZWGATZUFZAUHZWBMTZQZWDEFWKWKETFTUIZUGZQZUDZUJWFROWGBSZMW
      JWPWFRWJRNWQWJWGWGUKULZWGWGUKUMWIAWRWIWGWGWHUNWHWRNWGWGWHUOWGWGWHLUPZWSUQ
      URUSUTVCWQWKWJSZVAZWLWCWOWEXAWKCWBWTWQWKWJCWTVDWQWJBBWHUFZAUHCWQWIXBAWQWI
      XBVEWGBWGBWHVBVFVGJVHVIZVJXAWNDWDXAWNEFCCWMUGDXAEFWKWKWMCCWMXCXCXAWMVKVLK
      VHVJVNVMLEFAMVOWCWEVPVQVRVS $.
      $( [12-Jan-2015] $) $( [25-Feb-2008] $)
  $}

  ${
    $d f g x A $.  $d x F $.  $d x V $.
    symgbas.1 $e |- G = ( SymGrp ` A ) $.
    $( The base set of the symmetric group.  (Contributed by Mario Carneiro,
       12-Jan-2015.) $)
    symgbas $p |- ( A e. V -> { x | x : A -1-1-onto-> A } = ( Base ` G ) ) $=
      ( vf vg wcel cv wf1o cab cnx cbs cfv cop cplusg ccom cvv cmap eqid cpr co
      cmpt2 wceq wss wf f1of wb elmapg anidms syl5ibr abssdv ovex ssexg sylancl
      grpbase syl symgval fveq2d eqtr4d ) BDHZBBAIZJZAKZLMNVDOLPNFGVDVDFIGIQUCZ
      OUAZMNZCMNVAVDRHZVDVGUDVAVDBBSUBZUEVIRHVHVAVCAVIVCVBVIHZVABBVBUFZBBVBUGVA
      VJVKUHBBVBDDUIUJUKULBBSUMVDVIRUNUOVDVEVFRVFTUPUQVACVFMABVDVEFGCDEVDTVETUR
      USUT $.
      $( [12-Jan-2015] $)

    elsymgbas.2 $e |- B = ( Base ` G ) $.
    $( Two ways of saying a function is a 1-1-onto mapping of A to itself.
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    elsymgbas $p |- ( A e. V -> ( F e. B <-> F : A -1-1-onto-> A ) ) $=
      ( vx wcel cv wf1o cab cbs cfv symgbas syl6reqr eleq2d cvv wi syl cxp f1of
      wb wss wf fssxp xpexg anidms ssexg expcom syl5 f1oeq1 elab3g bitrd ) AEIZ
      CBICAAHJZKZHLZIZAACKZUOBURCUOURDMNBHADEFOGPQUOUTCRIZSUSUTUCUTCAAUAZUDZUOV
      AUTAACUEVCAACUBAACUFTUOVBRIZVCVASUOVDAAEEUGUHVCVDVACVBRUIUJTUKUQUTHCRAAUP
      CULUMTUN $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( The symmetric group on ` n ` objects has cardinality ` n ! ` .
       (Contributed by Mario Carneiro, 22-Jan-2015.) $)
    symghash $p |- ( A e. Fin -> ( # ` B ) = ( ! ` ( # ` A ) ) ) $=
      ( vf cfn wcel cv wf1o cab chash cfv cfa cbs symgbas syl6eqr fveq2d eqtr3d
      hashfac ) AGHZAAFIJFKZLMBLMALMNMUAUBBLUAUBCOMBFACGDPEQRAFTS $.
      $( [22-Jan-2015] $)
  $}

  ${
    $d f g x y A $.  $d f g x y B $.  $d f g x y V $.  $d x y X $.  $d x y Y $.
    symgplusg.1 $e |- G = ( SymGrp ` A ) $.
    symgplusg.2 $e |- B = ( Base ` G ) $.
    symgplusg.3 $e |- P = ( +g ` G ) $.
    $( The value of the symmetry group function at ` A ` .  (Contributed by
       Paul Chapman, 25-Feb-2008.) $)
    symgplusg $p |- ( A e. V -> P = ( f e. B , g e. B |-> ( f o. g ) ) ) $=
      ( vx wcel cplusg cfv cnx cbs cv cop eqid cvv wf1o cab cpr symgval symgbas
      ccom cmpt2 syl6eqr eqidd mpt2eq123dv opeq2d preq2d eqtrd fveq2d wceq fvex
      eqeltri mpt2ex grpplusg ax-mp 3eqtr4g ) AGLZFMNOPNAAKQUAKUBZRZOMNZDEBBDQE
      QUFZUGZRZUCZMNZCVGVBFVIMVBFVDVEDEVCVCVFUGZRZUCVIKAVCVKDEFGHVCSVKSUDVBVLVH
      VDVBVKVGVEVBDEVCVCVFBBVFVBVCFPNZBKAFGHUEIUHZVNVBVFUIUJUKULUMUNJVGTLVGVJUO
      DEBBVFBVMTIFPUPUQZVOURVCVGVITVISUSUTVA $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( The value of the group operation of the symmetry group on ` A ` .
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgov $p |- ( ( A e. V /\ X e. B /\ Y e. B ) ->
      ( X P Y ) = ( X o. Y ) ) $=
      ( vx vy wcel w3a cv ccom cvv cmpt2 wceq wa symgplusg simprl coeq1d simprr
      3ad2ant1 coeq2d eqtrd simp2 simp3 coexg 3adant1 ovmpt2d ) AEMZFBMZGBMZNZK
      LFGBBKOZLOZPZFGPZCQUMUNCKLBBUSRSUOABCKLDEHIJUAUEUPUQFSZURGSZTTZUSFURPUTVC
      UQFURUPVAVBUBUCVCURGFUPVAVBUDUFUGUMUNUOUHUMUNUOUIUNUOUTQMUMFGBBUJUKUL $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( The group operation of the symmetry group on ` A ` is closed, i.e. a
       magma.  (Contributed by Mario Carneiro, 12-Jan-2015.) $)
    symgcl $p |- ( ( A e. V /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( wcel w3a co ccom symgov wf1o wa wi f1oco elsymgbas a1i anbi12d 3imtr4d
      3impib eqeltrd ) AEKZFBKZGBKZLFGCMFGNZBABCDEFGHIJOUFUGUHUIBKZUFAAFPZAAGPZ
      QZAAUIPZUGUHQUJUMUNRUFAAAFGSUAUFUGUKUHULABFDEHITABGDEHITUBABUIDEHITUCUDUE
      $.
      $( [12-Jan-2015] $)
  $}

  ${
    $d x y z A $.  $d x y z G $.  $d x y z V $.
    symggrp.1 $e |- G = ( SymGrp ` A ) $.
    $( The symmetry group on ` A ` is a group.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    symggrp $p |- ( A e. V -> G e. Grp ) $=
      ( vx vy vz wcel cfv cv eqidd eqid symgcl wa co ccom wceq symgov elsymgbas
      wf1o cbs cplusg ccnv cid cres w3a coass 3adant3r3 coeq1d 3adant3r1 coeq2d
      3eqtr4a simpl simpr3 syl3anc simpr1 3eqtr4d mpbiri wi 3expia mpdan imp wf
      f1oi biimpa f1of fcoi2 3syl eqtrd f1ocnv 3imtr4d 3com23 mpd3an3 f1ococnv1
      a1i syl isgrpd ) ACHZEFGBUAIZBUBIZBEJZUCZUDAUEZVRVSKVRVTKAVSVTBCWAFJZDVSL
      ZVTLZMZVRWAVSHZWDVSHZGJZVSHZUFZNZWAWDVTOZWJPZWAWDWJVTOZPZWNWJVTOZWAWPVTOZ
      WMWAWDPZWJPWAWDWJPZPWOWQWAWDWJUGWMWNWTWJVRWHWIWNWTQWKAVSVTBCWAWDDWEWFRUHU
      IWMWPXAWAVRWIWKWPXAQWHAVSVTBCWDWJDWEWFRUJUKULWMVRWNVSHZWKWRWOQVRWLUMZVRWH
      WIXBWKWGUHVRWHWIWKUNAVSVTBCWNWJDWEWFRUOWMVRWHWPVSHZWSWQQXCVRWHWIWKUPVRWIW
      KXDWHAVSVTBCWDWJDWEWFMUJAVSVTBCWAWPDWEWFRUOUQVRWCVSHZAAWCTAVDAVSWCBCDWESU
      RZVRWHNZWCWAVTOZWCWAPZWAVRWHXHXIQZVRXEWHXJUSXFVRXEWHXJAVSVTBCWCWADWEWFRUT
      VAVBXGAAWATZAAWAVCXIWAQVRWHXKAVSWABCDWESZVEZAAWAVFAAWAVGVHVIVRWHWBVSHZVRX
      KAAWBTZWHXNXKXOUSVRAAWAVJVOXLAVSWBBCDWESVKVBZXGWBWAVTOZWBWAPZWCVRWHXNXQXR
      QZXPVRXNWHXSAVSVTBCWBWADWEWFRVLVMXGXKXRWCQXMAAWAVNVPVIVQ $.
      $( [13-Jan-2015] $) $( [25-Feb-2008] $)

    $( The value of the identity element of the symmetry group on ` A `
       (Contributed by Paul Chapman, 25-Jul-2008.) $)
    symgid $p |- ( A e. V -> ( _I |` A ) = ( 0g ` G ) ) $=
      ( wcel c0g cfv cid cres cplusg wceq ccom cbs wf1o f1oi eqid mpbiri symgov
      co elsymgbas mpd3an23 wf f1of fcoi1 mp2b cgrp symggrp grpid syl2anc mpbid
      syl6eq wb eqcomd ) ACEZBFGZHAIZUNUPUPBJGZSZUPKZUOUPKZUNURUPUPLZUPUNUPBMGZ
      EZVCURVAKUNVCAAUPNZAOZAVBUPBCDVBPZTQZVGAVBUQBCUPUPDVFUQPZRUAVDAAUPUBVAUPK
      VEAAUPUCAAUPUDUEUKUNBUFEVCUSUTULABCDUGVGVBUQBUOUPVFVHUOPUHUIUJUM $.
      $( [13-Jan-2015] $) $( [25-Feb-2008] $)
  $}

  ${
    $d w x y z G $.  $d w x y z M $.  $d w x y z V $.  $d w x y z X $.
    $d w z F $.  $d w x z H $.  $d w x y z Y $.
    galactghm.x $e |- X = ( Base ` G ) $.
    galactghm.h $e |- H = ( SymGrp ` Y ) $.
    galactghm.f $e |- F = ( x e. X |-> ( y e. Y |-> ( x M y ) ) ) $.
    $( The currying of a group action is a group homomorphism between the group
       ` G ` and the symetry group ` ( SymGrp `` Y ) ` .  (Contributed by FL,
       17-May-2010.)  (The proof was shortened by Mario Carneiro,
       13-Jan-2015.) $)
    galactghm $p |- ( ( M e. ( G GrpAct Y ) /\ Y e. V ) ->
      F e. ( G GrpHom H ) ) $=
      ( co wcel wa cfv wceq cmpt cvv syl2anc vz vw cga cgrp cbs wf cv wral cghm
      cplusg gagrp symggrp anim12i wf1o eqid 3expa wb elsymgbas ad2antlr mpbird
      gapm fmptd df-3an gaass sylan2br anassrs mpteq2dva ad2antrr simprl simprr
      w3a grpcl syl3anc mptexg oveq1 mpteq2dv fvmptg ccom simplr adantr ffvelrn
      symgov cxp gaf simpr fovrn oveq2 cbvmptv syl6eq fmptco 3eqtr4d ralrimivva
      eqtrd jca csymg fvex eqeltri isghm ax-mp sylanbrc ) FDIUCMNZIGNZOZDUDNZEU
      DNZOZHEUEPZCUFZUAUGZUBUGZDUJPZMZCPZXICPZXJCPZEUJPZMZQZUBHUHUAHUHZOZCDEUIM
      NZXAXDXBXEDFIUKZIEGKULUMXCXHXSXCAHBIAUGZBUGZFMZRZXGCXCYCHNZOYFXGNZIIYFUNZ
      XAXBYGYIBYCYFDFGHIJYFUOVAUPXBYHYIUQXAYGIXGYFEGKXGUOZURUSUTLVBZXCXRUAUBHHX
      CXIHNZXJHNZOZOZBIXLYDFMZRZBIXIXJYDFMZFMZRZXMXQYOBIYPYSXCYNYDINZYPYSQZYNUU
      AOXCYLYMUUAVKUUBYLYMUUAVCXIXJYDXKDFGHIJXKUOZVDVEVFVGYOXLHNZYQSNZXMYQQYOXD
      YLYMUUDXAXDXBYNYBVHXCYLYMVIZXCYLYMVJZHXKDXIXJJUUCVLVMXBUUEXAYNBIYPGVNUSAX
      LYFYQHSCYCXLQBIYEYPYCXLYDFVOVPLVQTYOXQXNXOVRZYTYOXBXNXGNZXOXGNZXQUUHQXAXB
      YNVSYOXHYLUUIXCXHYNYKVTZUUFHXGXICWATYOXHYMUUJUUKUUGHXGXJCWATIXGXPEGXNXOKY
      JXPUOZWBVMYOBAIIYRXIYCFMZYSXOXNYOUUAOHIWCIFUFZYMUUAYRINXCUUNYNUUADFGHIJWD
      VHYOYMUUAUUGVTYOUUAWEXJYDIHIFWFVMYOYMBIYRRZSNZXOUUOQUUGXBUUPXAYNBIYRGVNUS
      AXJYFUUOHSCYCXJQBIYEYRYCXJYDFVOVPLVQTYOXNBIXIYDFMZRZAIUUMRYOYLUURSNZXNUUR
      QUUFXBUUSXAYNBIUUQGVNUSAXIYFUURHSCYCXIQBIYEUUQYCXIYDFVOVPLVQTBAIUUQUUMYDY
      CXIFWGWHWIYCYRXIFWGWJWMWKWLWNESNYAXFXTOUQEIWOPSKIWOWPWQUBUAXKXPDECSHXGJYJ
      UUCUULWRWSWT $.
      $( [13-Jan-2015] $) $( [17-May-2010] $)
  $}

  ${
    $d u v x y z F $.  $d u v x y z G $.  $d u v x y z H $.  $d u v x y z V $.
    $d u v z M $.  $d u v x y X $.  $d u v x y z Y $.
    lactghmga.x $e |- X = ( Base ` G ) $.
    lactghmga.h $e |- H = ( SymGrp ` Y ) $.
    lactghmga.f $e |- M = ( x e. X , y e. Y |-> ( ( F ` x ) ` y ) ) $.
    $( The converse of ~ galactghm .  The uncurrying of a homomorphism into
       ` ( SymGrp `` Y ) ` is a group action.  Thus group actions and group
       homomorphisms into a symmetric group are essentially equivalent
       notions.  (Contributed by Mario Carneiro, 15-Jan-2015.) $)
    lactghmga $p |- ( ( F e. ( G GrpHom H ) /\ Y e. V ) ->
      M e. ( G GrpAct Y ) ) $=
      ( co wcel wa cfv wceq fveq2 fveq1d syl2anc vz vu vv cghm cga cgrp cxp c0g
      wf cv cplusg wral ghmgrp1 adantr wf1o cbs symggrp eqid ghmf ffvelrn sylan
      sylan2 wb elsymgbas ad2antlr mpbid f1of syl ralrimiva fmpt2 sylib grpidcl
      cid cres simpr fvex ovmpt2 symgid eqtr4d fvresi adantl 3eqtrd ccom simprr
      ghmid ad2antrr simpllr fvco3 simplll simprl ghmlin syl22anc syl3anc eqtrd
      symgov 3eqtr4d grpcl oveq2d ralrimivva jca w3a isga mpbir3and ) CDEUDMNZI
      GNZOZFDIUEMNZDUFNZHIUGIFUIZDUHPZUAUJZFMZXKQZUBUJZUCUJZDUKPZMZXKFMZXNXOXKF
      MZFMZQZUCHULUBHULZOZUAIULZXDXHXEDECUMUNZXFBUJZAUJZCPZPZINZBIULZAHULXIXFYK
      AHXFYGHNZOZYJBIYMIIYHUIZYFINYJYMIIYHUOZYNYMYHEUPPZNZYOXFHYPCUIZYLYQXEXDEU
      FNZYRIEGKUQZDECUFHYPJYPURZUSVBZHYPYGCUTVAXEYQYOVCXDYLIYPYHEGKUUAVDVEVFIIY
      HVGVHIIYFYHUTVAVIVIABHIYIIFLVJVKXFYCUAIXFXKINZOZXMYBUUDXLXKXJCPZPZXKVMIVN
      ZPZXKUUDXJHNZUUCXLUUFQXFUUIUUCXFXHUUIYEHDXJJXJURZVLVHUNXFUUCVOZABXJXKHIYI
      UUFFYFUUEPYGXJQYFYHUUEYGXJCRSYFXKUUERLXKUUEVPVQTUUDXKUUEUUGUUDUUEEUHPZUUG
      XFUUEUULQZUUCXEXDYSUUMYTDECUFXJUULUUJUULURWEVBUNXEUUGUULQXDUUCIEGKVRVEVSS
      UUCUUHXKQXFIXKVTWAWBUUDYAUBUCHHUUDXNHNZXOHNZOZOZXKXQCPZPZXNXKXOCPZPZFMZXR
      XTUUQXKXNCPZUUTWCZPZUVAUVCPZUUSUVBUUQIIUUTUIZUUCUVEUVFQUUQIIUUTUOZUVGUUQU
      UTYPNZUVHUUQYRUUOUVIXFYRUUCUUPUUBWFZUUDUUNUUOWDZHYPXOCUTTZUUQXEUVIUVHVCXD
      XEUUCUUPWGZIYPUUTEGKUUAVDVHVFIIUUTVGVHZUUDUUCUUPUUKUNZIIXKUVCUUTWHTUUQXKU
      URUVDUUQUURUVCUUTEUKPZMZUVDUUQXDYSUUNUUOUURUVQQXDXEUUCUUPWIUUQXEYSUVMYTVH
      UUDUUNUUOWJZUVKXPUVPDEXNCXOUFHJXPURZUVPURZWKWLUUQXEUVCYPNZUVIUVQUVDQUVMUU
      QYRUUNUWAUVJUVRHYPXNCUTTUVLIYPUVPEGUVCUUTKUUAUVTWOWMWNSUUQUUNUVAINZUVBUVF
      QUVRUUQUVGUUCUWBUVNUVOIIXKUUTUTTABXNUVAHIYIUVFFYFUVCPYGXNQYFYHUVCYGXNCRSY
      FUVAUVCRLUVAUVCVPVQTWPUUQXQHNZUUCXRUUSQUUQXHUUNUUOUWCXFXHUUCUUPYEWFUVRUVK
      HXPDXNXOJUVSWQWMUVOABXQXKHIYIUUSFYFUURPYGXQQYFYHUURYGXQCRSYFXKUURRLXKUURV
      PVQTUUQXSUVAXNFUUQUUOUUCXSUVAQUVKUVOABXOXKHIYIUVAFYFUUTPYGXOQYFYHUUTYGXOC
      RSYFXKUUTRLXKUUTVPVQTWRWPWSWTVIXEXGXHXIYDXAVCXDUAUBUCXPXJDFGHIJUVSUUJXBWA
      XC $.
      $( [15-Jan-2015] $)
  $}

  ${
    $d f x B $.  $d f x G $.  $d f x V $.  $d f x X $.
    symgga.g $e |- G = ( SymGrp ` X ) $.
    symgga.b $e |- B = ( Base ` G ) $.
    symgga.f $e |- F = ( f e. B , x e. X |-> ( f ` x ) ) $.
    $( The symmetric group induces a group action on its base set.
       (Contributed by Mario Carneiro, 24-Jan-2015.) $)
    symgga $p |- ( X e. V -> F e. ( G GrpAct X ) ) $=
      ( cid cres cghm co wcel cga cgrp cv cfv cmpt2 symggrp idghm syl wa fvresi
      wceq adantr fveq1d mpt2eq3ia eqtr4i lactghmga mpancom ) KBLZEEMNOZGFOZDEG
      PNOUOEQOUNGEFHUABEIUBUCCAUMEEDFBGIHDCABGARZCRZSZTCABGUPUQUMSZSZTJCABGUTUR
      UQBOZUPGOZUDUPUSUQVAUSUQUFVBBUQUEUGUHUIUJUKUL $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d a g x y G $.  $d g x H $.  $d a g x y P $.  $d a x U $.  $d a g x y X $.
    $d x F $.  $d x S $.
    cayleylem1.x $e |- X = ( Base ` G ) $.
    cayleylem1.p $e |- P = ( +g ` G ) $.
    cayleylem1.u $e |- U = ( 0g ` G ) $.
    cayleylem1.h $e |- H = ( SymGrp ` X ) $.
    cayleylem1.s $e |- S = ( Base ` H ) $.
    cayleylem1.f $e |- F = ( g e. X |-> ( a e. X |-> ( g P a ) ) ) $.
    $( Lemma for ~ cayley . $)
    cayleylem1 $p |- ( G e. Grp -> F e. ( G GrpHom H ) ) $=
      ( vx vy wcel co cv cgrp cghm eqidd cmpt2 cga cvv eqid gaid2 cbs fvex cmpt
      eqeltri oveq12 ovex ovmpt2a mpteq2dva mpteq2ia galactghm sylancl eqeltrrd
      cfv eqtr4i ) FUARZEEFGUBSZVCEUCVCPQHHPTZQTZASZUDZFHUESRHUFREVDRPQAVHFHJKV
      HUGZUHHFUIVAUFJFUIUJULDIEFGVHUFHHJMEDHIHDTZITZASZUKZUKDHIHVJVKVHSZUKZUKOD
      HVOVMVJHRIHVNVLPQVJVKHHVGVLVHVEVJVFVKAUMVIVJVKAUNUOUPUQVBURUSUT $.
      $( [13-Jan-2015] $) $( [3-Mar-2008] $)

    $( Lemma for ~ cayley . $)
    cayleylem2 $p |- ( G e. Grp -> F : X -1-1-> S ) $=
      ( vx wcel cfv wceq cvv cgrp wf1 cv c0g wi wral wa fveq1 co grpidcl adantr
      simpr grplactval syl2anc grprid eqtrd cid cres fvex eqeltri symgid fveq1i
      cbs ax-mp fvresi syl5eqr eqeq12d syl5ib ralrimiva cghm cayleylem1 symggrp
      syl wb eqid ghmf1 sylancl mpbird ) FUAQZHBEUBZPUCZERZGUDRZSZWACSZUEZPHUFZ
      VSWFPHWDCWBRZCWCRZSVSWAHQZUGZWECWBWCUHWKWHWAWICWKWHWACAUIZWAWKWJCHQZWHWLS
      VSWJULVSWMWJHFCJLUJUKZWACADEFHIOJUMUNHAFCWAJKLUOUPWKWICUQHURZRZCCWOWCHTQZ
      WOWCSHFVCRTJFVCUSUTZHGTMVAVDVBWKWMWPCSWNHCVEVMVFVGVHVIVSEFGVJUIQGUAQZVTWG
      VNABCDEFGHIJKLMNOVKWQWSWRHGTMVLVDPFGWCEUAHBCJNLWCVOVPVQVR $.
      $( [13-Jan-2015] $) $( [3-Mar-2008] $)
  $}

  ${
    $d a f g s G $.  $d f g s H $.  $d a g P $.  $d a f g s X $.
    cayley.x $e |- X = ( Base ` G ) $.
    cayley.h $e |- H = ( SymGrp ` X ) $.
    ${
      cayley.p $e |- P = ( +g ` G ) $.
      cayley.f $e |- F = ( g e. X |-> ( a e. X |-> ( g P a ) ) ) $.
      cayley.s $e |- S = ran F $.
      $( Cayley's Theorem (constructive version): given group ` G ` , ` F ` is
         an isomorphism between ` G ` and the subgroup ` S ` of the symmetry
         group ` H ` on the underlying set ` X ` of ` G ` .  (Contributed by
         Paul Chapman, 3-Mar-2008.)  (The proof was shortened by Mario
         Carneiro, 13-Jan-2015.) $)
      cayley $p |- ( G e. Grp -> ( S e. ( SubGrp ` H ) /\
        F e. ( G GrpHom ( H |`s S ) ) /\ F : X -1-1-onto-> S ) ) $=
        ( cgrp wcel cfv co cbs eqid cvv csubg cghm wf1o crn c0g cayleylem1 fvex
        cress eqeltri symggrp ax-mp ghmrn sylancl syl5eqel wb eqimss2i resghm2b
        wss a1i syl2anc mpbid wf1 cayleylem2 f1f1orn wceq f1oeq3 sylibr 3jca
        syl ) ENOZBFUAPZOZDEFBUHQZUBQOZGBDUCZVJBDUDZVKMVJDEFUBQOZFNOZVPVKOAFRPZ
        EUEPZCDEFGHIKVTSZJVSSZLUFZGTOVRGERPTIERUGUIGFTJUJUKEFDNULUMUNZVJVQVNWCV
        JVLVPBURZVQVNUOWDWEVJBVPMUPUSEFVMDBVMSUQUTVAVJGVPDUCZVOVJGVSDVBWFAVSVTC
        DEFGHIKWAJWBLVCGVSDVDVIBVPVEVOWFUOMBVPGDVFUKVGVH $.
        $( [13-Jan-2015] $) $( [3-Mar-2008] $)
    $}

    $( Cayley's Theorem (existence version): every group ` G ` is isomorphic to
       a subgroup of the symmetry group on the underlying set of ` G ` .  (For
       any group ` G ` there exists an isomorphism ` f ` between ` G ` and a
       subgroup ` h ` of the symmetry group on the underlying set of ` G ` .)
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    cayleyth $p |- ( G e. Grp -> E. s e. ( SubGrp ` H )
      E. f e. ( G GrpHom ( H |`s s ) ) f : X -1-1-onto-> s ) $=
      ( vg va wcel cv cfv co cmpt wf1o cress cghm wrex eqid rcla4ev cgrp cplusg
      csubg cayley simp1d simp2d simp3d f1oeq1 syl2anc wceq oveq2 oveq2d f1oeq3
      crn rexeqbidv ) BUAJZHDIDHKIKBUBLZMNNZUNZCUCLZJZDUSAKZOZABCUSPMZQMZRZDEKZ
      VBOZABCVGPMZQMZRZEUTRUPVAURVEJZDUSUROZUQUSHURBCDIFGUQSURSUSSUDZUEUPVLVMVF
      UPVAVLVMVNUFUPVAVLVMVNUGVCVMAURVEDUSVBURUHTUIVKVFEUSUTVGUSUJZVHVCAVJVEVOV
      IVDBQVGUSCPUKULVGUSDVBUMUOTUI $.
      $( [13-Jan-2015] $) $( [3-Mar-2008] $)
  $}


