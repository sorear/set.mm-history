$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c e f g m n p s w x y $.
    $( Define class of all groups. $)
    df-grp $a |- Grp = { f | E. g E. p ( g = ( Base ` f )
       /\ p = ( +g ` f ) /\ ( A. a e. g A. b e. g A. c e. g ( ( a p b ) e. g
            /\ ( ( a p b ) p c ) = ( a p ( b p c ) ) ) /\
          E. e e. g A. a e. g ( ( e p a ) = a /\
            E. m e. g ( m p a ) = e ) ) ) } $.

    $( Define group identity element. $)
    df-0g $a |- 0g = ( g e. _V |-> ( iota_ e e. ( Base ` g )
      A. x e. ( Base ` g )
        ( ( e ( +g ` g ) x ) = x /\ ( x ( +g ` g ) e ) = x ) ) ) $.

    $( Define inverse of group element. $)
    df-minusg $a |- invg = ( g e. _V |-> ( x e. ( Base ` g )
  |-> ( iota_ w e. ( Base ` g ) ( w ( +g ` g ) x ) = ( 0g ` g ) ) ) ) $.

    $( Define group subtraction (also called division for multiplicative
       groups). $)
    df-sbg $a |- -g = ( g e. _V |-> ( x e. ( Base ` g ) , y e. ( Base ` g )
  |-> ( x ( +g ` g ) ( ( invg ` g ) ` y ) ) ) ) $.

    $( Define the group multiple function, also known as group exponentiation
       when viewed multiplicatively. $)
    df-mulg $a |- .g = ( g e. _V |-> ( n e. ZZ , x e. ( Base ` g ) |->
    if ( n = 0 , ( 0g ` g ) , [_ seq 1 ( ( +g ` g ) , ( NN X. { x } ) ) / s ]_
      if ( 0 < n , ( s ` n ) , ( ( invg ` g ) ` ( s ` -u n ) ) ) ) ) ) $.
  $}

  ${
    $d a b c e f g m p $.
    $( Alternate definition of the class of all groups using class
       substitution. $)
    dfgrp2 $p |- Grp = { f | [ ( Base ` f ) / g ] [ ( +g ` f ) / p ]
      ( A. a e. g A. b e. g A. c e. g ( ( a p b ) e. g
            /\ ( ( a p b ) p c ) = ( a p ( b p c ) ) ) /\
          E. e e. g A. a e. g ( ( e p a ) = a /\
            E. m e. g ( m p a ) = e ) ) } $=
      ( cv cbs cfv wceq cplusg co wa wral wrex wex cab wsbc cgrp wcel fvex sbc5
      w3a df-grp 19.42v 3anass exbii anbi2i 3bitr4ri bitri abbii eqtr4i ) UACIZ
      BIZJKZLZEIZUPMKZLZFIZGIZUSNZUOUBVDHIZUSNVBVCVEUSNUSNLOHUOPGUOPFUOPAIZVBUS
      NVBLDIVBUSNVFLDUOQOFUOPAUOQOZUEZERZCRZBSVGEUTTZCUQTZBSABCDEFGHUFVLVJBVLUR
      VKOZCRVJVKCUQUPJUCUDVMVICURVAVGOZOZERURVNERZOVIVMURVNEUGVHVOEURVAVGUHUIVK
      VPURVGEUTUPMUCUDUJUKUIULUMUN $.
      $( [2-Dec-2013] $)
  $}

  ${
    $d a b c e f g m p B $.  $d f g p G $.  $d a b c e f g m p P $.
    isgrp.b $e |- B = ( Base ` G ) $.
    isgrp.p $e |- P = ( +g ` G ) $.
    $( The predicate "is a group." $)
    isgrp $p |- ( G e. Grp <-> ( G e. _V
            /\ A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( vg vp wcel cv wceq co wa wral wrex oveq cgrp cbs cfv cplusg w3a wex cab
      vf cvv df-grp eleq2i fveq2 syl6eqr eqeq2d 3anbi12d 2exbidv elab4g eqeltri
      eqid fvex eleq2 anbi1d raleqbi1dv anbi2d rexeqbi1dv anbi12d eleq1d oveq1d
      oveq2d eqeq12d ralbidv 2ralbidv eqeq1d rexbidv rexralbidv ceqsex2v anbi2i
      rexeq eqtrd 3anass bitr4i 3bitri ) EUAMEKNZUHNZUBUCZOZLNZWDUDUCZOZFNZGNZW
      GPZWCMZWLHNZWGPZWJWKWNWGPZWGPZOZQZHWCRZGWCRZFWCRZCNZWJWGPZWJOZDNZWJWGPZXC
      OZDWCSZQZFWCRZCWCSZQZUEZLUFKUFZUHUGZMEUIMZWCAOZWGBOZXMUEZLUFKUFZQZXQWJWKB
      PZAMZYCWNBPZWJWKWNBPZBPZOZQZHARZGARFARZXCWJBPZWJOZXFWJBPZXCOZDASZQZFARCAS
      ZUEZUAXPECUHKDLFGHUJUKXOYAUHEXPWDEOZXNXTKLYTWFXRWIXSXMYTWEAWCYTWEEUBUCZAW
      DEUBULIUMUNYTWHBWGYTWHEUDUCZBWDEUDULJUMUNUOUPXPUSUQYBXQYKYRQZQYSYAUUCXQXM
      WLAMZWRQZHARZGARZFARZXEXHDASZQZFARZCASZQUUCKLABAUUAUIIEUBUTURBUUBUIJEUDUT
      URXRXBUUHXLUULXAUUGFWCAWTUUFGWCAWSUUEHWCAXRWMUUDWRWCAWLVAVBVCVCVCXKUUKCWC
      AXJUUJFWCAXRXIUUIXEXHDWCAVRVDVCVEVFXSUUHYKUULYRXSUUFYJFGAAXSUUEYIHAXSUUDY
      DWRYHXSWLYCAWJWKWGBTZVGXSWOYEWQYGXSWOWLWNBPYEWLWNWGBTXSWLYCWNBUUMVHVSXSWQ
      WJWPBPYGWJWPWGBTXSWPYFWJBWKWNWGBTVIVSVJVFVKVLXSUUJYQCFAAXSXEYMUUIYPXSXDYL
      WJXCWJWGBTVMXSXHYODAXSXGYNXCXFWJWGBTVMVNVFVOVFVPVQXQYKYRVTWAWB $.
      $( [17-Oct-2012] $)

    $( The predicate "is a group." $)
    isgrp2 $p |- ( G e. Grp <-> ( A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( cgrp wcel cv co wceq wa wral wrex c0 cbs cvv elex rexn0 wn fvprc syl5eq
      wne cfv necon1ai syl adantl wb wi w3a isgrp pm4.71ri 3anass 3bitr3i mpbir
      pm5.32 pm5.21nii ) EKLZEUALZFMZGMZBNZALVFHMZBNVDVEVGBNBNOPHAQGAQFAQZCMZVD
      BNVDODMVDBNVIODARPFAQZCARZPZEKUBZVKVCVHVKASUGVCVJCAUCVCASVCUDAETUHSIETUEU
      FUIUJUKVCVBVLULUMVCVBPZVCVLPZULVBVCVHVKUNVNVOABCDEFGHIJUOVBVCVMUPVCVHVKUQ
      URVCVBVLUTUSVA $.
      $( [24-Oct-2012] $)
  $}

  ${
    $d u w x B $.  $d u w x P $.
    $( A two-sided identity element is unique (if it exists) in any magma.
       (Contributed by Mario Carneiro, 7-Dec-2014.) $)
    mgmidmo $p |- E* u ( u e. B /\
      A. x e. B ( ( u P x ) = x /\ ( x P u ) = x ) ) $=
      ( vw cv wcel co wceq wa wral ralimi oveq1 id eqeq12d rcla4va oveq2 eqeq1d
      wal an4s wmo simpl simpr anim12i eqtr2 syl2an ancom2s sylan2 gen2 anbi12d
      wi eleq1 ralbidv mo4 mpbir ) BFZCGZUPAFZDHZURIZURUPDHZURIZJZACKZJZBUAVEEF
      ZCGZVFURDHZURIZURVFDHZURIZJZACKZJZJUPVFIZUKZESBSVPBEUQVGVDVMVOVDVMJUQVGJZ
      UTACKZVKACKZJVOVDVRVMVSVCUTACUTVBUBLVLVKACVIVKUCLUDVQVSVRVOUQVSVGVRVOUQVS
      JUPVFDHZUPIZVTVFIZVOVGVRJVKWAAUPCURUPIZVJVTURUPURUPVFDMWCNOPUTWBAVFCURVFI
      ZUSVTURVFURVFUPDQWDNOPVTUPVFUEUFTUGUHTUIVEVNBEVOUQVGVDVMUPVFCULVOVCVLACVO
      UTVIVBVKVOUSVHURUPVFURDMRVOVAVJURUPVFURDQRUJUMUJUNUO $.
      $( [7-Dec-2014] $)
  $}

  ${
    $d a b c e m u w x y z B $.  $d u w x y z G $.  $d a b c e m u w x y z P $.
    $d w x y A $.  $d w y ph $.  $d w x y z U $.  $d a b c y X $.  $d b c Y $.
    $d y ps $.  $d c Z $.
    grplem1.b $e |- B = ( Base ` G ) $.
    grplem1.p $e |- P = ( +g ` G ) $.
    $( Lemma for group properties. $)
    grplem1 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
       ( ( X P Y ) e. B /\ ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) ) $=
      ( va vb vc ve vm wcel cv co wceq wa wral oveq1 cgrp w3a cvv isgrp simp2bi
      wrex eleq1d oveq1d eqeq12d anbi12d oveq2 oveq2d anbi2d rcla43v mpan9 ) CU
      ANZIOZJOZBPZANZUSKOZBPZUQURVABPZBPZQZRZKASJASIASZDANEANFANUBDEBPZANZVHFBP
      ZDEFBPZBPZQZRZUPCUCNVGLOZUQBPUQQMOUQBPVOQMAUFRIASLAUFABLMCIJKGHUDUEVFVNDU
      RBPZANZVPVABPZDVCBPZQZRVIVHVABPZDEVABPZBPZQZRIJKDEFAAAUQDQZUTVQVEVTWEUSVP
      AUQDURBTZUGWEVBVRVDVSWEUSVPVABWFUHUQDVCBTUIUJUREQZVQVIVTWDWGVPVHAUREDBUKZ
      UGWGVRWAVSWCWGVPVHVABWHUHWGVCWBDBUREVABTULUIUJVAFQZWDVMVIWIWAVJWCVLVAFVHB
      UKWIWBVKDBVAFEBUKULUIUMUNUO $.
      $( [14-Aug-2011] $)

    $( Closure of the operation of a group. $)
    grpcl $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( cgrp wcel co w3a 3anass anidm anbi2i bitri wceq grplem1 simpld sylan2br
      wa 3impb ) CHIZDAIZEAIZDEBJZAIZUCUDTZUBUCUDUDKZUFUHUCUDUDTZTUGUCUDUDLUIUD
      UCUDMNOUBUHTUFUEEBJDEEBJBJPABCDEEFGQRSUA $.
      $( [14-Aug-2011] $)

    $( A group operation is associative. $)
    grpass $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
              ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) $=
      ( cgrp wcel w3a wa co wceq grplem1 simprd ) CIJDAJEAJFAJKLDEBMZAJQFBMDEFB
      MBMNABCDEFGHOP $.
      $( [14-Aug-2011] $)

    $( A group has a left identity element, and every member has a left
       inverse. $)
    grplidinv $p |- ( G e. Grp ->
         E. u e. B A. x e. B ( ( u P x ) = x /\ E. y e. B ( y P x ) = u ) ) $=
      ( vb vc cgrp wcel cvv cv co wceq wa wral wrex isgrp simp3bi ) FKLFMLANZIN
      ZEOZDLUDJNZEOUBUCUEEOEOPQJDRIDRADRCNZUBEOUBPBNUBEOUFPBDSQADRCDSDECBFAIJGH
      TUA $.
      $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem1 $p |- ( ( ( G e. Grp /\ ( Y e. B /\ A e. B ) ) /\
                  ( ( Y P A ) = U /\ ( A P A ) = A ) ) -> ( U P A ) = U ) $=
      ( cgrp wcel wa co wceq oveq1 ad2antrl w3a id 3anidm23 grpass sylan2 oveq2
      adantr eqtr3d ad2antll simprl 3eqtrd ) EIJZFBJZABJZKZKZFACLZDMZAACLZAMZKZ
      KZDACLZFUNCLZULDUQULACLZURUSUMUTURMUKUOULDACNOUKUTUSMZUPUJUGUHUIUIPZVAUHU
      IVBVBQRBCEFAAGHSTUBUCUOUSULMUKUMUNAFCUAUDUKUMUOUEUF $.
      $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem2 $p |- ( ( ( G e. Grp /\ ( Y e. B /\ A e. B ) ) /\ ( (
 U P Y ) = Y /\ ( Y P A ) = U ) ) -> ( ( A P Y ) P ( A P Y ) ) = ( A P Y ) ) $=
      ( cgrp wcel wa co wceq w3a simprr simprl grpcl 3com23 3expb grpass syldan
      3jca adantr oveq1 adantl simpl eqtr2d id 3anidm13 sylan2 sylan9eqr eqcomd
      oveq2d eqtrd ) EIJZFBJZABJZKZKZDFCLZFMZFACLZDMZKZKZAFCLZVFCLZAFVFCLZCLZVF
      USVGVIMZVDUOURUQUPVFBJZNVJUSUQUPVKUOUPUQOUOUPUQPUOUPUQVKUOUQUPVKBCEAFGHQR
      SUBBCEAFVFGHTUAUCVEVHFACVEFVHVDUSFVBFCLZVHVDVLUTFVCVLUTMVAVBDFCUDUEVAVCUF
      UGURUOUPUQUPNZVLVHMUPUQVMVMUHUIBCEFAFGHTUJUKULUMUN $.
      $( [16-Aug-2011] $)

    ${
      grpidinvlem3.ph $e |- ( ph <-> A. x e. B ( U P x ) = x ) $.
      grpidinvlem3.ps $e |- ( ps <-> A. x e. B E. z e. B ( z P x ) = U ) $.
      $( Lemma for ~ grpidinv . $)
      grpidinvlem3 $p |- ( ( ( ( G e. Grp /\ U e. B ) /\ ( ph /\ ps ) )
               /\ A e. B ) -> E. y e. B ( ( y P A ) = U /\ ( A P y ) = U ) ) $=
        ( vw wcel wa co wceq wrex cgrp cv wral oveq1 eqeq1d cbvrexv bitri oveq2
        ralbii rexbidv rcla4cva sylanb adantll grpcl adantllr ad2antrl ad2antrr
        3expa biimpi eqeq12d rcla4va syl2anc adantr pm3.22 an31s adantlr anim1i
        id adantlll grpidinvlem2 3expb ad2ant2rl sylan2b anass an32s syldan imp
        wi ex grpidinvlem1 sylan exp43 rexlimdv syl5 mpand exp32 com34 impl mpd
        imp32 eqtr3d ancld reximdva ) JUAPZIGPZQZABQZQZFGPZQZDUBZFHRZISZDGTZXCF
        XAHRZISZQZDGTWQWSXDWPBWSXDABXACUBZHRZISZDGTZCGUCZWSXDBEUBZXHHRZISZEGTZC
        GUCZXLNXPXKCGXOXJEDGXMXASXNXIIXMXAXHHUDUEUFUIUGXKXDCFGXHFSZXJXCDGXRXIXB
        IXHFXAHUHUEUJUKULUMUMWTXCXGDGWTXAGPZQZXCXFXTXCXFXTXCQZIXEHRZXEIXTYBXESZ
        XCXTXEGPZIXHHRZXHSZCGUCZYCWPWSXSYDWQWNWSXSYDWOWNWSXSYDGHJFXAKLUNZURUOUO
        WRYGWSXSAYGWPBAYGMUSUPUQYFYCCXEGXHXESZYEYBXHXEXHXEIHUHYIVHUTVAVBVCYAXEX
        EHRXESZYBISZYAWNXSWSQZQZIXAHRZXASZXCQYJXTYMXCWPWSXSYMWQWNWSXSYMWOXSWSWN
        YMYLWNVDVEUOUOVCXTYOXCWQWSXSYOWPWQXSYOWSAXSYOBAYGXSYOMYFYOCXAGXHXASZYEY
        NXHXAXHXAIHUHYPVHUTUKULVFVFVIVGFGHIJXAKLVJVBXTYJYKVRZXCWRWSXSYQWPABWSXS
        QZYQVRWPAYRBYQWPAYRBYQVRWPAYRQQZYDBYQWNYRYDWOAWNWSXSYDYHVKZVLYDBQOUBZXE
        HRZISZOGTZYSYQBYDUUAXHHRZISZOGTZCGUCZUUDBXQUUHNXPUUGCGXOUUFEOGXMUUASXNU
        UEIXMUUAXHHUDUEUFUIUGUUGUUDCXEGYIUUFUUCOGYIUUEUUBIXHXEUUAHUHUEUJVAVMYSU
        UCYQOGYSUUAGPZUUCYJYKYSUUIQWNUUIYDQQZUUCYJQYKYSUUIUUJWNYRUUIUUJVRZWOAWN
        YRYDUUKYTWNYDQUUIUUJWNUUIYDUUJWNUUIQYDQUUJWNUUIYDVNUSVOVSVPVLVQXEGHIJUU
        AKLVTWAWBWCWDWEWFWGWJWHVCWIWKVSWLWMWI $.
        $( [16-Aug-2011] $)
    $}

    $d y G $.  $d y U $.  $d y X $.
    $( Lemma for ~ grpidinv . $)
    grpidinvlem4 $p |- ( ( ( G e. Grp /\ X e. B ) /\ E. y e. B
             ( ( y P X ) = U /\ ( X P y ) = U ) ) -> ( X P U ) = ( U P X ) ) $=
      ( cgrp wcel wa cv co wceq wrex simpll simplr simpr grpass syl13anc anasss
      oveq2 sylan9eq oveq1 sylan9req exp31 rexlimdv imp ) EIJZFBJZKZALZFCMZDNZF
      ULCMZDNZKZABOFDCMZDFCMZNZUKUQUTABUKULBJZUQUTUKVAKZUNUPUTVBUNKUPURUOFCMZUS
      VBUNVCFUMCMZURVBUIUJVAUJVCVDNUIUJVAPUIUJVAQZUKVARVEBCEFULFGHSTUMDFCUBUCUO
      DFCUDUEUAUFUGUH $.
      $( [16-Aug-2011] $)

    $( A group has a left and right identity element, and every member has a
       left and right inverse. $)
    grpidinv $p |- ( G e. Grp -> E. u e. B A. x e. B ( ( ( u P x ) = x /\
         ( x P u ) = x ) /\ E. y e. B ( ( y P x ) = u /\ ( x P y ) = u ) ) ) $=
      ( vz vw wcel cv co wceq wrex wa wral simpl ralimi id cgrp grplidinv oveq2
      eqeq12d rcla4cva sylan adantll anim1i adantrr adantr ad2antlr simpr jca32
      adantl biid grpidinvlem3 grpidinvlem4 syl2anc eqtrd jca31 ralrimiva exp32
      sylancom reximdvai mpd ) FUAKZCLZILZEMZVHNZJLVHEMVGNJDOZPZIDQZCDOVGALZEMZ
      VNNZVNVGEMZVNNZPBLZVNEMVGNVNVSEMVGNPBDOZPZADQZCDOIJCDEFGHUBVFVMWBCDVFVGDK
      ZVMWBVFWCVMPZPZWAADWEVNDKZPZVPVRVTWDWFVPVFVMWFVPWCVMVJIDQZWFVPVLVJIDVJVKR
      SZVJVPIVNDVHVNNZVIVOVHVNVHVNVGEUCWJTUDUEUFUGUGZWGVQVOVNWGVFWFPVTVQVONWEVF
      WFVFWDRUHWEWFVFWCPZWHVKIDQZPPVTWGWLWHWMWEWLWFVFWCWLVMWLTUIUJWDWHVFWFVMWHW
      CWIUNUKWDWMVFWFVMWMWCVLVKIDVJVKULSUNUKUMWHWMIBJVNDEVGFGHWHUOWMUOUPVCZBDEV
      GFVNGHUQURWKUSWNUTVAVBVDVE $.
      $( [16-Aug-2011] $)

    $( The two-sided identity element of a group is unique.  Lemma 2.2.1(a) of
       [Herstein] p. 55.  (Contributed by Mario Carneiro, 8-Dec-2014.) $)
    grpideu $p |- ( G e. Grp ->
      E! u e. B A. x e. B ( ( u P x ) = x /\ ( x P u ) = x ) ) $=
      ( vy cgrp wcel cv co wceq wa wral wrex wmo wreu grpidinv simpl ralimi syl
      reximi mgmidmo a1i reu5 sylanbrc ) EIJZBKZAKZDLUJMUJUIDLUJMNZACOZBCPZUICJ
      ULNBQZULBCRUHUKHKZUJDLUIMUJUODLUIMNHCPZNZACOZBCPUMAHBCDEFGSURULBCUQUKACUK
      UPTUAUCUBUNUHABCDUDUEULBCUFUG $.
      $( [8-Dec-2014] $)

    $( The two-sided identity element of a group is unique.  Lemma 2.2.1(a) of
       [Herstein] p. 55. $)
    grpideu2 $p |- ( G e. Grp -> E! u e. B A. x e. B ( u P x ) = x ) $=
      ( vw vz vy wcel cv co wceq wral wa wrex oveq2 id eqeq12d cgrp wi grpidinv
      simpll ralimi cbvralv sylib adantl ad2antlr simpr anbi12d rexbidv rcla4va
      eqeq1d oveq1 adantll sylan2 grpidinvlem4 syldan adantllr adantr ad2ant2rl
      wreu an32s ad2ant2lr 3eqtr3d ex mpand ralrimiva jca reximdva ralbidv reu8
      mpd sylibr ) EUAKZBLZALZDMZVRNZACOZHLZVRDMZVRNZACOZVQWBNZUBZHCOZPZBCQZWAB
      CVCVPVQILZDMZWKNZWKVQDMWKNZPZJLZWKDMZVQNZWKWPDMZVQNZPZJCQZPZICOZBCQWJIJBC
      DEFGUCVPXDWIBCVPVQCKZPZXDWIXFXDPZWAWHXDWAXFXDWMICOWAXCWMICWMWNXBUDUEWMVTI
      ACWKVRNZWLVSWKVRWKVRVQDRXHSTUFUGZUHXGWGHCXGWBCKZPZWAWEWFXDWAXFXJXIUIXKWAW
      EPZWFXKXLPWBVQDMZVQWBDMZVQWBXKXMXNNZXLVPXDXJXOXEVPXJXDXOVPXJPZXDWPWBDMZVQ
      NZWBWPDMZVQNZPZJCQZXOXDXPXBICOZYBXCXBICWOXBUJUEXJYCYBVPXBYBIWBCWKWBNZXAYA
      JCYDWRXRWTXTYDWQXQVQWKWBWPDRUNYDWSXSVQWKWBWPDUOUNUKULUMUPUQJCDVQEWBFGURUS
      VDUTVAXFXJXLXMVQNZXDXFWEYEXJWAXEWEYEVPWDYEAVQCVRVQNZWCXMVRVQVRVQWBDRYFSTU
      MUPVBUTXJWAXNWBNZXGWEVTYGAWBCVRWBNZVSXNVRWBVRWBVQDRYHSTUMVEVFVGVHVIVJVGVK
      VNWAWEBHCWFVTWDACWFVSWCVRVQWBVRDUOUNVLVMVO $.
      $( [16-Aug-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.  $d x y G $.  $d x y ph $.  $d y Y $.
    $d x y X $.
    $( Lemma for ~ grppropd . $)
    proplem2 $p |- ( ( ( X e. A /\ Y e. A ) /\
      A. x e. A A. y e. A ( x F y ) e. A ) -> ( X F Y ) e. A ) $=
      ( cv co wcel wceq oveq1 eleq1d oveq2 rcla42va ) AGZBGZDHZCIEFDHZCIEPDHZCI
      ABEFCCOEJQSCOEPDKLPFJSRCPFEDMLN $.
      $( [6-Dec-2014] $)

    proplem.1 $e |- ( ( ph /\ ( x e. A /\ y e. B ) ) ->
                         ( x F y ) = ( x G y ) ) $.
    $( Lemma for ~ grppropd . $)
    proplem $p |- ( ( ph /\ ( X e. A /\ Y e. B ) ) ->
                         ( X F Y ) = ( X G Y ) ) $=
      ( cv co wceq wral wcel wa ralrimivva oveq1 eqeq12d oveq2 rcla42v impcom
      sylan ) ABKZCKZFLZUDUEGLZMZCENBDNZHDOIEOPZHIFLZHIGLZMZAUHBCDEJQUJUIUMUHUM
      HUEFLZHUEGLZMBCHIDEUDHMUFUNUGUOUDHUEFRUDHUEGRSUEIMUNUKUOULUEIHFTUEIHGTSUA
      UBUC $.
      $( [6-Dec-2014] $)
  $}

  ${
    $d s t u v w x y B $.  $d s t u v w x y K $.  $d s t u v w x y ph $.
    $d s t u v w x y L $.
    grppropd.1 $e |- ( ph -> B = ( Base ` K ) ) $.
    grppropd.2 $e |- ( ph -> B = ( Base ` L ) ) $.
    grppropd.3 $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
      ( x ( +g ` K ) y ) = ( x ( +g ` L ) y ) ) $.
    $( If two structures have the same group components (properties), one is a
       group iff the other one is.  (Contributed by Stefan O'Rear,
       27-Nov-2014.) $)
    grppropd $p |- ( ph -> ( K e. Grp <-> L e. Grp ) ) $=
      ( vu vv vw vs vt co wcel wral wa wceq wrex cv cplusg cgrp simplr ad2antrr
      cfv cbs simprl eleqtrd simprr eqid syl3anc eleqtrrd ralrimivva ex adantlr
      grpcl eleq12d mpbird wb proplem adantr eleq1d simplll simplrl idi simplrr
      wi simpllr proplem2 syl21anc simpr syl12anc oveq1d oveq2d eqeq12d anbi12d
      eqtrd ralbidva 2ralbidva eleq2d anbi1d raleqbidv 3bitr3d rexbidva rexeqdv
      eqeq1d anbi2d rexeqbidv isgrp2 3bitr4g pm5.21ndd ) ABUAZCUAZEUBUFZOZDPZCD
      QBDQZEUCPZFUCPZAWSWRAWSRZWQBCDDXAWMDPZWNDPZRZRZWPEUGUFZDXEWSWMXFPWNXFPWPX
      FPAWSXDUDXEWMDXFXAXBXCUHADXFSZWSXDGUEZUIXEWNDXFXAXBXCUJXHUIXFWOEWMWNXFUKZ
      WOUKZUQULXHUMUNUOAWTWRAWTRZWQBCDDXKXDRZWQWMWNFUBUFZOZFUGUFZPZXLWTWMXOPWNX
      OPXPAWTXDUDXLWMDXOXKXBXCUHADXOSZWTXDHUEZUIXLWNDXOXKXBXCUJXRUIXOXMFWMWNXOU
      KZXMUKZUQULXLWPXNDXOAXDWPXNSWTIUPXRURUSUNUOAWRWSWTUTAWRRZJUAZKUAZWOOZXFPZ
      YDLUAZWOOZYBYCYFWOOZWOOZSZRZLXFQZKXFQZJXFQZMUAZYBWOOZYBSZNUAZYBWOOZYOSZNX
      FTZRZJXFQZMXFTZRYBYCXMOZXOPZUUEYFXMOZYBYCYFXMOZXMOZSZRZLXOQZKXOQZJXOQZYOY
      BXMOZYBSZYRYBXMOZYOSZNXOTZRZJXOQZMXOTZRWSWTYAYNUUNUUDUVBYAYDDPZYJRZLDQZKD
      QZJDQUUEDPZUUJRZLDQZKDQZJDQYNUUNYAUVEUVIJKDDYAYBDPZYCDPZRZRZUVDUVHLDUVNYF
      DPZRZUVCUVGYJUUJUVPYDUUEDUVNYDUUESZUVOAUVMUVQWRABCDDWOXMYBYCIVAUPVBZVCUVP
      YGUUGYIUUIUVPYGYDYFXMOZUUGUVPAUVCUVOYGUVSSAWRUVMUVOVDZUVPUVKUVLWRUVCUVPUV
      KVHYAUVKUVLUVOVEZVFUVPUVLVHYAUVKUVLUVOVGZVFAWRUVMUVOVIZBCDWOYBYCVJVKUVNUV
      OVLZABCDDWOXMYDYFIVAVMUVPYDUUEYFXMUVRVNVRUVPYIYBYHXMOZUUIUVPAUVKYHDPZYIUW
      ESUVTUWAUVPUVLUVOWRUWFUWBUWDUWCBCDWOYCYFVJVKABCDDWOXMYBYHIVAVMUVPYHUUHYBX
      MUVPAUVLUVOYHUUHSUVTUWBUWDABCDDWOXMYCYFIVAVMVOVRVPVQVSVTYAUVFYMJDXFAXGWRG
      VBZYAUVEYLKDXFUWGYAUVDYKLDXFUWGYAUVCYEYJYADXFYDUWGWAWBWCWCWCYAUVJUUMJDXOA
      XQWRHVBZYAUVIUULKDXOUWHYAUVHUUKLDXOUWHYAUVGUUFUUJYADXOUUEUWHWAWBWCWCWCWDY
      AYQYTNDTZRZJDQZMDTUUPUURNDTZRZJDQZMDTUUDUVBYAUWKUWNMDYAYODPZRZUWJUWMJDUWP
      UVKRZYQUUPUWIUWLUWQYPUUOYBUWQAUWOUVKYPUUOSAWRUWOUVKVDZYAUWOUVKUDUWPUVKVLZ
      ABCDDWOXMYOYBIVAVMWGUWQYTUURNDUWQYRDPZRZYSUUQYOUXAAUWTUVKYSUUQSUWQAUWTUWR
      VBUWQUWTVLUWQUVKUWTUWSVBABCDDWOXMYRYBIVAVMWGWEVQVSWEYAUWKUUCMDXFUWGYAUWJU
      UBJDXFUWGYAUWIUUAYQYAYTNDXFUWGWFWHWCWIYAUWNUVAMDXOUWHYAUWMUUTJDXOUWHYAUWL
      UUSUUPYAUURNDXOUWHWFWHWCWIWDVQXFWOMNEJKLXIXJWJXOXMMNFJKLXSXTWJWKUOWL $.
      $( [27-Nov-2014] $)
  $}

  ${
    $d x y K $.  $d x y L $.
    grpprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    grpprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    $( If two structures have the same group components (properties), one is a
       group iff the other one is. $)
    grpprop $p |- ( K e. Grp <-> L e. Grp ) $=
      ( vx vy cgrp wcel wb wtru cbs cfv eqidd wceq a1i cv cplusg co wa oveqi
      grppropd trud ) AGHBGHIJEFAKLZABJUCMUCBKLNJCOEPZFPZAQLZRUDUEBQLZRNJUDUCHU
      EUCHSSUFUGUDUEDTOUAUB $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    elgrp.b $e |- B e. _V $.
    elgrp.p $e |- P e. _V $.
    elgrp.k $e |- K
          = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( The predicate "is a group structure" for a specific structure member. $)
    elgrp $p |- ( K e. Grp <-> ( A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( cvv wcel cbs cfv wceq grpbase ax-mp cplusg grpplusg isgrp2 ) ABCDEFGHAL
      MAENOPIABELKQRBLMBESOPJABELKTRUA $.
      $( [24-Oct-2012] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    grppropOLD.b $e |- ( Base ` K ) = B $.
    grppropOLD.p $e |- ( +g ` K ) = P $.
    grppropOLD.c $e |- ( Base ` L ) = B $.
    grppropOLD.q $e |- ( +g ` L ) = P $.
    $( Any set with the same (relevant) properties as a given group is also a
       group (and vice-versa). $)
    grppropOLD $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm wcel cv co wceq wral cfv eqcomi cbs cplusg isgrp2 bitr4i
      cgrp wa wrex ) CUENIOZJOZBPZANUJKOZBPUHUIUKBPBPQUFKARJARIARLOZUHBPUHQMOUH
      BPULQMAUGUFIARLAUGUFDUENABLMCIJKCUASAETCUBSBFTUCABLMDIJKDUASAGTDUBSBHTUCU
      D $.
      $( [28-Oct-2012] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    grpstr.b $e |- ( Base ` K ) = B $.
    grpstr.p $e |- ( +g ` K ) = P $.
    grpstr.l $e |- L
          = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( Generalize a specific 2-element group ` L ` to show that any set ` K `
       with the same (relevant) properties is also a group. $)
    grpstr $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm cgrp wcel cv co wceq wa wral wrex cbs eqcomi cplusg fvex
      cfv isgrp2 cvv eqeltrri elgrp bitr4i ) CMNHOZIOZBPZANUMJOZBPUKULUNBPBPQRJ
      ASIASHASKOZUKBPUKQLOUKBPUOQLATRHASKATRDMNABKLCHIJCUAUEZAEUBCUCUEZBFUBUFAB
      KLDHIJUPAUGECUAUDUHUQBUGFCUCUDUHGUIUJ $.
      $( [28-Oct-2012] $)
  $}

  ${
    grpss.g $e |- G = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    grpss.r $e |- R e. _V $.
    grpss.s $e |- G C_ R $.
    grpss.f $e |- Fun R $.
    $( Show that a structure extending a constructed group (e.g. a ring) is
       also a group.  This allows us to prove that a constructed potential ring
       ` R ` is a group before we know that it is also a ring.  (Theorem
       ~ rnggrp , on the other hand, requires that we know in advance that
       ` R ` is a ring.) $)
    grpss $p |- ( G e. Grp <-> R e. Grp ) $=
      ( vs cgrp wcel cbs baseid cnx cfv cop cplusg opex eleqtrri strss plusgid
      cpr prid1 prid2 grpprop bicomi ) CJKDJKCDADCLIFHGIMNLOZAPZUHNQOZBPZUBZDUH
      UJUGARUCESTBDCQIFHGIUAUJUKDUHUJUIBRUDESTUEUF $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d u x y B $.  $d u x y G $.
    grpbn0.b $e |- B = ( Base ` G ) $.
    $( The base set of a group is not empty.  (Contributed by Szymon
       Jaroszewicz, 3-Apr-2007.) $)
    grpbn0 $p |- ( G e. Grp -> B =/= (/) ) $=
      ( vu vx vy cgrp wcel cv cplusg cfv co wceq wrex wa wral c0 eqid grplidinv
      wne rexn0 syl ) BGHDIZEIZBJKZLUDMFIUDUELUCMFANOEAPZDANAQTEFDAUEBCUERSUFDA
      UAUB $.
      $( [18-Oct-2012] $)
  $}

  $( A group is not empty.  (Contributed by Mario Carneiro, 2-Dec-2014.) $)
  grpn0 $p |- ( G e. Grp -> G =/= (/) ) $=
    ( vx cgrp wcel cbs cfv c0 wne eqid grpbn0 wceq fveq2 baseid syl6eqr necon3i
    cnx str0 syl ) ACDAEFZGHAGHSASIJAGSGAGKSGEFGAGELEPEFBBMQNOR $.
    $( [2-Dec-2014] $)

  ${
    $d e m x y z B $.  $d e m x y z G $.  $d m N $.  $d e m P $.  $d e m x Z $.
    $d e m x y z ph $.
    isgrpd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrpd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrpd.g $e |- ( ph -> G e. _V ) $.
    isgrpd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpd.z $e |- ( ph -> Z e. B ) $.
    isgrpd.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    isgrpd.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpd.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Deduce a group from its properties. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpd $p |- ( ph -> G e. Grp ) $=
      ( co wceq ve vm cvv wcel cv cplusg cfv cbs wa wral wrex w3a 3adant3r3 jca
      cgrp ralrimivvva eqeq1d rcla4ev syl2anc ralrimiva rexbidv anbi12d ralbidv
      oveq1 eqeq2 3jca oveqd eleq12d eqidd oveq123d eqeq12d raleqbidv rexeqbidv
      3anbi23d mpbid eqid isgrp sylibr ) AGUCUDZBUEZCUEZGUFUGZSZGUHUGZUDZWCDUEZ
      WBSZVTWAWFWBSZWBSZTZUIZDWDUJZCWDUJZBWDUJZUAUEZVTWBSZVTTZUBUEZVTWBSZWOTZUB
      WDUKZUIZBWDUJZUAWDUKZULZGUOUDAVSVTWAFSZEUDZXFWFFSZVTWAWFFSZFSZTZUIZDEUJZC
      EUJZBEUJZWOVTFSZVTTZWRVTFSZWOTZUBEUKZUIZBEUJZUAEUKZULXEAVSXOYCLAXLBCDEEEA
      VTEUDZWAEUDZWFEUDZULUIXGXKAYDYEXGYFMUMNUNUPAIEUDIVTFSZVTTZXRITZUBEUKZUIZB
      EUJZYCOAYKBEAYDUIZYHYJPYMHEUDHVTFSZITZYJQRYIYOUBHEWRHTXRYNIWRHVTFVDUQURUS
      UNUTYBYLUAIEWOITZYAYKBEYPXQYHXTYJYPXPYGVTWOIVTFVDUQYPXSYIUBEWOIXRVEVAVBVC
      URUSVFAXOWNYCXDVSAXNWMBEWDJAXMWLCEWDJAXLWKDEWDJAXGWEXKWJAXFWCEWDAFWBVTWAK
      VGZJVHAXHWGXJWIAXFWCWFWFFWBKYQAWFVIVJAVTVTXIWHFWBKAVTVIAFWBWAWFKVGVJVKVBV
      LVLVLAYBXCUAEWDJAYAXBBEWDJAXQWQXTXAAXPWPVTAFWBWOVTKVGUQAXSWTUBEWDJAXRWSWO
      AFWBWRVTKVGUQVMVBVLVMVNVOWDWBUAUBGBCDWDVPWBVPVQVR $.
      $( [6-Jun-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d y z N $.  $d x O $.  $d x y z P $.
    $d x y z ph $.
    isgrprd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrprd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrprd.g $e |- ( ph -> G e. _V ) $.
    isgrprd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrprd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrprd.z $e |- ( ph -> O e. B ) $.
    isgrprd.i $e |- ( ( ph /\ x e. B ) -> ( x P O ) = x ) $.
    isgrprd.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrprd.j $e |- ( ( ph /\ x e. B ) -> ( x P N ) = O ) $.
    $( Deduce a group from its properties.  Same as ~ isgrpd except with right
       identity and inverse. ` N ` (negative) is normally dependent on ` x `
       i.e. read it as ` N ( x ) ` . $)
    isgrprd $p |- ( ph -> G e. Grp ) $=
      ( grplidd grplinvd isgrpd ) ABCDEFGHIJKLMNOABCDEFHIMOPNQRSQABCDEFHIMOPNQR
      TUA $.
      $( [10-Aug-2013] $)
  $}

  ${
    $d e m x y z B $.  $d m N $.  $d e m x y z P $.  $d e m x Z $.
    $d x y z ph $.
    isgrpdOLD.b $e |- B = ( Base ` G ) $.
    isgrpdOLD.p $e |- P = ( +g ` G ) $.
    isgrpdOLD.g $e |- ( ph -> G e. _V ) $.
    isgrpdOLD.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpdOLD.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpdOLD.z $e |- ( ph -> Z e. B ) $.
    isgrpdOLD.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    isgrpdOLD.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpdOLD.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpdOLD $p |- ( ph -> G e. Grp ) $=
      ( wcel co ve vm cvv cv wceq wa wral wrex w3a wi 3adant3r3 jca 3exp2 imp3a
      ralrimdv ralrimivv oveq1 eqeq1d rcla4ev syl2anc ralrimiva rexbidv anbi12d
      cgrp eqeq2 ralbidv 3jca isgrp sylibr ) AGUCSZBUDZCUDZFTZESZVMDUDZFTVKVLVO
      FTFTUEZUFZDEUGZCEUGBEUGZUAUDZVKFTZVKUEZUBUDZVKFTZVTUEZUBEUHZUFZBEUGZUAEUH
      ZUIGVDSAVJVSWILAVRBCEEAVKESZVLESZUFVQDEAWJWKVOESZVQUJAWJWKWLVQAWJWKWLUIUF
      VNVPAWJWKVNWLMUKNULUMUNUOUPAIESIVKFTZVKUEZWDIUEZUBEUHZUFZBEUGZWIOAWQBEAWJ
      UFZWNWPPWSHESHVKFTZIUEZWPQRWOXAUBHEWCHUEWDWTIWCHVKFUQURUSUTULVAWHWRUAIEVT
      IUEZWGWQBEXBWBWNWFWPXBWAWMVKVTIVKFUQURXBWEWOUBEVTIWDVEVBVCVFUSUTVGEFUAUBG
      BCDJKVHVI $.
      $( [5-Jun-2013] $)
  $}

  ${
    $d e m x y z B $.  $d m N $.  $d e m x y z P $.  $d e m x Z $.
    isgrpi.g $e |- G e. _V $.
    isgrpi.b $e |- B = ( Base ` G ) $.
    isgrpi.p $e |- P = ( +g ` G ) $.
    isgrpi.c $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpi.a $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpi.z $e |- Z e. B $.
    isgrpi.i $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpi.n $e |- ( x e. B -> N e. B ) $.
    isgrpi.j $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` .  OBSOLETE PROOF. $)
    isgrpiOLD $p |- G e. Grp $=
      ( wcel co wceq ve vm cgrp cvv cv wa wral wrex isgrp w3a 3adant3 jca rgen3
      oveq1 eqeq1d rcla4ev syl2anc eqeq2 rexbidv anbi12d ralbidv mp2an mpbir3an
      rgen ) FUCRFUDRAUEZBUEZESZDRZVGCUEZESVEVFVIESESTZUFZCDUGBDUGADUGUAUEZVEES
      ZVETZUBUEZVEESZVLTZUBDUHZUFZADUGZUADUHZDEUAUBFABCJKUIIVKABCDDDVEDRZVFDRZV
      IDRZUJVHVJWBWCVHWDLUKMULUMHDRHVEESZVETZVPHTZUBDUHZUFZADUGZWANWIADWBWFWHOW
      BGDRGVEESZHTZWHPQWGWLUBGDVOGTVPWKHVOGVEEUNUOUPUQULVDVTWJUAHDVLHTZVSWIADWM
      VNWFVRWHWMVMWEVEVLHVEEUNUOWMVQWGUBDVLHVPURUSUTVAUPVBVC $.
      $( [3-Sep-2011] $)

    $d x y z G $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpi $p |- G e. Grp $=
      ( wcel wtru co cgrp cbs cfv wceq a1i cplusg cvv 3adant1 w3a adantl isgrpd
      cv trud ) FUARSABCDEFGHDFUBUCUDSJUEEFUFUCUDSKUEFUGRSIUEAULZDRZBULZDRZUNUP
      ETZDRSLUHUOUQCULZDRUIURUSETUNUPUSETETUDSMUJHDRSNUEUOHUNETUNUDSOUJUOGDRSPU
      JUOGUNETHUDSQUJUKUM $.
      $( [3-Sep-2011] $)
  $}

  ${
    $d x y z B $.  $d x y z g G $.  $d x y z P $.  $d x Z $.
    isgrpix.a $e |- B e. _V $.
    isgrpix.b $e |- P e. _V $.
    isgrpix.g $e |- G = { <. 1 , B >. , <. 2 , P >. } $.
    isgrpix.2 $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpix.3 $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpix.z $e |- Z e. B $.
    isgrpix.5 $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpix.6 $e |- ( x e. B -> N e. B ) $.
    isgrpix.7 $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group.  Read ` N ` as ` N ( x ) ` .  Note:
       This theorem has hard-coded structure indices for demonstration
       purposes.  It is not intended for general use. $)
    isgrpix $p |- G e. Grp $=
      ( c1 cop c2 cpr cvv prex eqeltri grpbasex grpplusgx isgrpi ) ABCDEFGHFRDS
      ZTESZUAUBKUHUIUCUDDEFIJKUEDEFIJKUFLMNOPQUG $.
      $( [4-Sep-2011] $)
  $}

  ${
    $d e g x B $.  $d e g x G $.  $d g P $.
    grpidval.b $e |- B = ( Base ` G ) $.
    grpidval.p $e |- P = ( +g ` G ) $.
    grpidval.o $e |- O = ( 0g ` G ) $.
    $( The value of the identity element of a group. $)
    grpidval $p |- ( G e. V -> O =
      ( iota_ e e. B A. x e. B ( ( e P x ) = x /\ ( x P e ) = x ) ) ) $=
      ( vg wcel cvv cv co wceq wa cfv cplusg cbs wral crio elex c0g fveq2 oveqd
      syl6eqr eqeq1d anbi12d raleqbidv riotaeqbidv df-0g riotaex fvmpt syl5eq
      syl ) EGLEMLZFDNZANZCOZUSPZUSURCOZUSPZQZABUAZDBUBZPEGUCUQFEUDRVFJKEURUSKN
      ZSRZOZUSPZUSURVHOZUSPZQZAVGTRZUAZDVNUBVFMUDVGEPZVOVEDVNBVPVNETRBVGETUEHUG
      ZVPVMVDAVNBVQVPVJVAVLVCVPVIUTUSVPVHCURUSVPVHESRCVGESUEIUGZUFUHVPVKVBUSVPV
      HCUSURVRUFUHUIUJUKADKULVEDBUMUNUOUP $.
      $( [7-Aug-2013] $) $( [20-Aug-2011] $)
  $}

  ${
    $d e x y B $.  $d e x G $.  $d e x O $.  $d e x y P $.  $d e x y U $.
    $d x X $.
    grpidcl.b $e |- B = ( Base ` G ) $.
    grpidcl.o $e |- O = ( 0g ` G ) $.
    ${
      mgmidcl.p $e |- P = ( +g ` G ) $.
      mgmidcl.e $e |- ( ph ->
        E. e e. B A. x e. B ( ( e P x ) = x /\ ( x P e ) = x ) ) $.
      $( The identity element of a magma, if it exists, belongs to the base
         set. $)
      ismgmid $p |- ( ph -> ( ( U e. B /\
        A. x e. B ( ( U P x ) = x /\ ( x P U ) = x ) ) <-> O = U ) ) $=
        ( wcel cv co wceq wa eqeq1d syl c0 vy wral crio wreu wb id wrex mgmidmo
        wmo a1i reu5 sylanbrc ax-17 oveq1 oveq2 anbi12d ralbidv riota2f syl2anr
        a17d pm5.32da riotacl eleq1 syl5ibcom pm4.71rd bitr4d cvv wne rexn0 cbs
        wn cfv fvprc syl5eq necon1ai grpidval ) AECMZEBNZDOZVRPZVREDOZVRPZQZBCU
        BZQZFNZVRDOZVRPZVRWFDOZVRPZQZBCUBZFCUCZEPZHEPAWEVQWNQWNAVQWDWNVQVQWLFCU
        DZWDWNUEAVQUFAWLFCUGZWFCMWLQFUIZWOLWQABFCDUHUJWLFCUKULZWLWDFUACEUANEMFU
        MVQWDFUTWFEPZWKWCBCWSWHVTWJWBWSWGVSVRWFEVRDUNRWSWIWAVRWFEVRDUORUPUQURUS
        VAAWNVQAWMCMZWNVQAWOWTWRWLFCVBSWMECVCVDVEVFAHWMEAGVGMZHWMPACTVHZXAAWPXB
        LWLFCVISXACTXAVKCGVJVLTIGVJVMVNVOSBCDFGHVGIKJVPSRVF $.
        $( [27-Dec-2014] $)

      $( The identity element of a magma, if it exists, belongs to the base
         set. $)
      mgmidcl $p |- ( ph -> O e. B ) $=
        ( wcel cv co wceq wa wral eqid ismgmid mpbiri simpld ) AGCLZGBMZDNUCOUC
        GDNUCOPBCQZAUBUDPGGOGRABCDGEFGHIJKSTUA $.
        $( [27-Dec-2014] $)

      $( The identity element of a magma, if it exists, is a left and right
         identity. $)
      mgmlrid $p |- ( ( ph /\ X e. B ) ->
        ( ( O P X ) = X /\ ( X P O ) = X ) ) $=
        ( cv co wceq wa wral wcel eqid eqeq12d ismgmid mpbiri simprd id anbi12d
        oveq2 oveq1 rcla4cva sylan ) AGBMZDNZUJOZUJGDNZUJOZPZBCQZHCRGHDNZHOZHGD
        NZHOZPZAGCRZUPAVBUPPGGOGSABCDGEFGIJKLUAUBUCUOVABHCUJHOZULURUNUTVCUKUQUJ
        HUJHGDUFVCUDZTVCUMUSUJHUJHGDUGVDTUEUHUI $.
        $( [27-Dec-2014] $)
    $}

    $( The identity element of a group belongs to the group. $)
    grpidcl $p |- ( G e. Grp -> O e. B ) $=
      ( vx ve cgrp wcel cplusg cfv eqid cv co wceq wa wral wreu wrex grpideu
      reurex syl mgmidcl ) BHIZFABJKZGBCDEUELZUDGMZFMZUENUHOUHUGUENUHOPFAQZGARU
      IGASFGAUEBDUFTUIGAUAUBUC $.
      $( [27-Dec-2014] $) $( [27-Aug-2011] $)
  $}

  ${
    $d x y X $.  $d u w x y B $.  $d u x y G $.  $d u w x y P $.
    $d u w x y O $.
    grpidinv2.b $e |- B = ( Base ` G ) $.
    grpidinv2.p $e |- P = ( +g ` G ) $.
    grpidinv2.o $e |- O = ( 0g ` G ) $.
    $( X group's properties using the explicit identity element. $)
    grpidinv2 $p |- ( ( G e. Grp /\ X e. B ) -> ( ( ( O P X ) = X /\
         ( X P O ) = X ) /\ E. y e. B ( ( y P X ) = O /\ ( X P y ) = O ) ) ) $=
      ( vx vu wcel cv co wceq wa wrex wral eqeq1d anbi12d vw cgrp crio grpidinv
      wreu grpideu wi ssid simpl ralimi rgenw riotass2 mpanl12 syl2anc grpidval
      wss eqtr4d wb grpidcl reuss2 ax-17 a17d oveq1 oveq2 eqeq2 rexbidv ralbidv
      riota2f mpbird id eqeq12d rcla4cva sylan ) DUBLZEJMZCNZVOOZVOECNZVOOZPZAM
      ZVOCNZEOZVOWACNZEOZPZABQZPZJBRZFBLEFCNZFOZFECNZFOZPZWAFCNZEOZFWACNZEOZPZA
      BQZPZVNWIKMZVOCNZVOOZVOXBCNZVOOZPZWBXBOZWDXBOZPZABQZPZJBRZKBUCZEOZVNXNXGJ
      BRZKBUCZEVNXMKBQZXPKBUEZXNXQOZJAKBCDGHUDZJKBCDGHUFZBBUPZXMXPUGZKBRZXRXSPZ
      XTBUHZYDKBXLXGJBXGXKUIUJUKZXMXPKBBULUMUNJBCKDEUBGHIUOUQVNEBLZXMKBUEZWIXOU
      RBDEGIUSVNXRXSYJYAYBYCYEYFYJYGYHXMXPKBBUTUMUNXMWIKUABEUAMELKVAYIWIKVBXBEO
      ZXLWHJBYKXGVTXKWGYKXDVQXFVSYKXCVPVOXBEVOCVCSYKXEVRVOXBEVOCVDSTYKXJWFABYKX
      HWCXIWEXBEWBVEXBEWDVETVFTVGVHUNVIWHXAJFBVOFOZVTWNWGWTYLVQWKVSWMYLVPWJVOFV
      OFECVDYLVJZVKYLVRWLVOFVOFECVCYMVKTYLWFWSABYLWCWPWEWRYLWBWOEVOFWACVDSYLWDW
      QEVOFWACVCSTVFTVLVM $.
      $( [18-Aug-2011] $)

    $( The identity element of a group is a left identity. $)
    grplid $p |- ( ( G e. Grp /\ X e. B ) -> ( O P X ) = X ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2 simpll syl ) CJKEAKLDEBMENZED
      BMENZLIOZEBMDNEUBBMDNLIAPZLTIABCDEFGHQTUAUCRS $.
      $( [18-Aug-2011] $)

    $( The identity element of a group is a right identity. $)
    grprid $p |- ( ( G e. Grp /\ X e. B ) -> ( X P O ) = X ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2 simplr syl ) CJKEAKLDEBMENZED
      BMENZLIOZEBMDNEUBBMDNLIAPZLUAIABCDEFGHQTUAUCRS $.
      $( [18-Aug-2011] $)

    $d e B $.  $d e G $.  $d e P $.  $d e x w Z $.
    $( Properties showing that an element ` Z ` is the identity element of a
       group.  See also ~ isgrpid2 . $)
    isgrpid $p |- ( G e. Grp
  -> ( ( Z e. B /\ A. x e. B ( Z P x ) = x ) <-> O = Z ) ) $=
      ( ve vw cgrp wcel cv co wceq wral wa crio wreu wb id grpideu2 ax-17 oveq1
      a17d eqeq1d ralbidv riota2f syl2anr grpidval wrex grpideu reurex syl ssid
      wss wi simpl ralimi rgenw riotass2 mpanl12 syl2anc adantr bitr4d pm5.32da
      eqtrd grpidcl eleq1 syl5ibcom pm4.71rd ) DLMZFBMZFANZCOZVOPZABQZRVNEFPZRV
      SVMVNVRVSVMVNRVRJNZVOCOZVOPZABQZJBSZFPZVSVNVNWCJBTZVRWEUAVMVNUBAJBCDGHUCZ
      WCVRJKBFKNFMJUDVNVRJUFVTFPZWBVQABWHWAVPVOVTFVOCUEUGUHUIUJVMVSWEUAVNVMEWDF
      VMEWBVOVTCOVOPZRZABQZJBSZWDABCJDELGHIUKVMWKJBULZWFWLWDPZVMWKJBTWMAJBCDGHU
      MWKJBUNUOWGBBUQWKWCURZJBQWMWFRWNBUPWOJBWJWBABWBWIUSUTVAWKWCJBBVBVCVDVHUGV
      EVFVGVMVSVNVMEBMVSVNBDEGIVIEFBVJVKVLVF $.
      $( [8-Dec-2014] $) $( [7-Aug-2013] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.  $d y Y $.  $d y Z $.
    grprcan.b $e |- B = ( Base ` G ) $.
    grprcan.p $e |- P = ( +g ` G ) $.
    $( Right cancellation law for groups. $)
    grprcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( X P Z ) = ( Y P Z ) <-> X = Y ) ) $=
      ( vy wcel co wceq wa wrex adantl oveq1 grpass 3exp2 adantrr 3eqtr3d wb cv
      cgrp c0g wi eqid grpidinv2 simpr reximi ad2ant2rl ad2antll imp41 adantlrl
      cfv imp42 adantllr adantrrl oveq2 ad2antrl grprid ad2antrr ad2ant2r exp45
      syl adantr rexlimdv mpd impbid1 exp43 3imp2 ) CUCJZDAJZEAJZFAJZDFBKZEFBKZ
      LZDELZUAZVKVLVMVNVSVKVLMZVMVNMZMZVQVRWBFIUBZBKZCUDUNZLZIANZVQVRUEZVKVNWGV
      LVMVKVNMWEFBKFLFWEBKFLMZWCFBKWELZWFMZIANZMWGIABCWEFGHWEUFZUGWLWGWIWKWFIAW
      JWFUHUIOVDUJWBWFWHIAWBWCAJZWFVQVRWBWNWFVQMMZMZDWEBKZEWEBKZDEWPDWDBKZEWDBK
      ZWQWRWBWNVQWSWTLWFWBWNVQMMVOWCBKZVPWCBKZWSWTVQXAXBLWBWNVOVPWCBPUKWBWNXAWS
      LZVQVTVNWNXCVMVKVLVNWNXCVKVLVNWNXCABCDFWCGHQRULUMSWBWNXBWTLZVQVKWAWNXDVLV
      KVMVNWNXDVKVMVNWNXDABCEFWCGHQRUOUPSTUQWOWSWQLZWBWFXEWNVQWDWEDBURUSOWOWTWR
      LZWBWFXFWNVQWDWEEBURUSOTVTWQDLWAWOABCWEDGHWMUTVAWBWRELZWOVKVMXGVLVNABCWEE
      GHWMUTVBVETVCVFVGDEFBPVHVIVJ $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d y z B $.  $d y z G $.  $d y z P $.  $d y z O $.  $d y z X $.
    grpinveu.b $e |- B = ( Base ` G ) $.
    grpinveu.p $e |- P = ( +g ` G ) $.
    grpinveu.o $e |- O = ( 0g ` G ) $.
    $( The left inverse element of a group is unique.  Lemma 2.2.1(b) of
       [Herstein] p. 55. $)
    grpinveu $p |- ( ( G e. Grp /\ X e. B )
           -> E! y e. B ( y P X ) = O ) $=
      ( vz cgrp wcel wa cv co wceq wi wral wrex wreu grpidinv2 simpl reximi syl
      adantl eqtr3 grprcan syl5ib 3exp2 com24 imp41 an32s exp3a ralrimdva ancld
      w3a reximdva mpd oveq1 eqeq1d reu8 sylibr ) DKLZFBLZMZANZFCOZEPZJNZFCOZEP
      ZVFVIPZQZJBRZMZABSZVHABTVEVHABSZVPVEEFCOFPFECOFPMZVHFVFCOEPZMZABSZMVQABCD
      EFGHIUAWAVQVRVTVHABVHVSUBUCUEUDVEVHVOABVEVFBLZMZVHVNWCVHVMJBWCVIBLZMVHVKV
      LVEWDWBVHVKMZVLQZVCVDWDWBWFVCWBWDVDWFVCWBWDVDWFWEVGVJPVCWBWDVDUPMVLVGVJEU
      FBCDVFVIFGHUGUHUIUJUKULUMUNUOUQURVHVKAJBVLVGVJEVFVIFCUSUTVAVB $.
      $( [24-Aug-2011] $)

    $( Two ways of saying that an element of a group is the identity element.
       Provides a convenient way to compute the value of the identity
       element. $)
    grpid $p |- ( ( G e. Grp /\ X e. B ) ->
                  ( ( X P X ) = X <-> O = X ) ) $=
      ( wceq cgrp wcel wa co eqcom wb wi grpidcl grprcan 3exp2 mpid pm2.43d imp
      grplid eqeq2d bitr3d syl5rbb ) DEIEDIZCJKZEAKZLZEEBMZEIZDENUJUKDEBMZIZUGU
      LUHUIUNUGOZUHUIUOUHUIDAKZUIUOPACDFHQUHUIUPUIUOABCEDEFGRSTUAUBUJUMEUKABCDE
      FGHUCUDUEUF $.
      $( [24-Aug-2011] $)

    $( Obsolete proof of ~ grpid as of 9-Mar-2014. $)
    grpidOLD $p |- ( ( G e. Grp /\ X e. B ) ->
                  ( X = O <-> ( X P X ) = X ) ) $=
      ( cgrp wcel wa co wceq wb wi grpidcl grprcan 3exp2 mpid pm2.43d grplid
      imp eqeq2d bitr3d ) CIJZEAJZKZEEBLZDEBLZMZEDMZUHEMUEUFUJUKNZUEUFULUEUFDAJ
      ZUFULOACDFHPUEUFUMUFULABCEDEFGQRSTUBUGUIEUHABCDEFGHUAUCUD $.
      $( [24-Aug-2011] $)

    $( Properties showing that an element ` Z ` is the identity element of a
       group. $)
    isgrpid2 $p |- ( G e. Grp
  -> ( ( Z e. B /\ ( Z P Z ) = Z ) <-> O = Z ) ) $=
      ( cgrp wcel co wceq wa grpid biimpd expimpd grpidcl grplid mpdan jca id
      eleq1 oveq12d eqeq12d anbi12d syl5ibcom impbid ) CIJZEAJZEEBKZELZMZDELZUH
      UIUKUMUHUIMUKUMABCDEFGHNOPUHDAJZDDBKZDLZMUMULUHUNUPACDFHQZUHUNUPUQABCDDFG
      HRSTUMUNUIUPUKDEAUBUMUOUJDEUMDEDEBUMUAZURUCURUDUEUFUG $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g O $.  $d g P $.
    grpinvval.b $e |- B = ( Base ` G ) $.
    grpinvval.p $e |- P = ( +g ` G ) $.
    grpinvval.o $e |- O = ( 0g ` G ) $.
    grpinvval.n $e |- N = ( invg ` G ) $.
    $( The inverse function of a group. $)
    grpinvfval $p |- ( G e. V
              -> N = ( x e. B |-> ( iota_ y e. B ( y P x ) = O ) ) ) $=
      ( vg cvv cv wceq cfv cbs cplusg c0g wcel co crio cmpt cminusg fveq2 oveqd
      elex syl6eqr eqeq12d riotaeqbidv mpteq12dv df-minusg eqeltri mptex syl5eq
      fvex fvmpt syl ) EHUAENUAZFACBOZAOZDUBZGPZBCUCZUDZPEHUHUTFEUEQVFLMEAMOZRQ
      ZVAVBVGSQZUBZVGTQZPZBVHUCZUDVFNUEVGEPZAVHVMCVEVNVHERQZCVGERUFIUIZVNVLVDBV
      HCVPVNVJVCVKGVNVIDVAVBVNVIESQDVGESUFJUIUGVNVKETQGVGETUFKUIUJUKULABMUMACVE
      CVONIERUQUNUOURUPUS $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)

    $d x O $.  $d x P $.  $d x y X $.
    $( The inverse of a group element. $)
    grpinvval $p |- ( ( G e. V /\ X e. B ) ->
                    ( N ` X ) = ( iota_ y e. B ( y P X ) = O ) ) $=
      ( vx wcel cfv cv co wceq crio cmpt grpinvfval fveq1d oveq2 riotabidv eqid
      eqeq1d riotaex fvmpt sylan9eq ) DGNZHBNHEOHMBAPZMPZCQZFRZABSZTZOUKHCQZFRZ
      ABSZUJHEUPMABCDEFGIJKLUAUBMHUOUSBUPULHRZUNURABUTUMUQFULHUKCUCUFUDUPUEURAB
      UGUHUI $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g I $.  $d g P $.
    grpsubval.b $e |- B = ( Base ` G ) $.
    grpsubval.p $e |- P = ( +g ` G ) $.
    grpsubval.i $e |- I = ( invg ` G ) $.
    grpsubval.m $e |- M = ( -g ` G ) $.
    $( Group subtraction (division) operation. $)
    grpsubfval $p |- ( G e. V
              -> M = ( x e. B , y e. B |-> ( x P ( I ` y ) ) ) ) $=
      ( vg cvv cv cfv cbs cminusg cplusg fveq2 wcel cmpt2 wceq elex csg syl6eqr
      eqidd fveq1d oveq123d mpt2eq123dv df-sbg fvex eqeltri mpt2ex fvmpt syl5eq
      co syl ) EHUAENUAZGABCCAOZBOZFPZDUQZUBZUCEHUDUSGEUEPVDLMEABMOZQPZVFUTVAVE
      RPZPZVESPZUQZUBVDNUEVEEUCZABVFVFVJCCVCVKVFEQPZCVEEQTIUFZVMVKUTUTVHVBVIDVK
      VIESPDVEESTJUFVKUTUGVKVAVGFVKVGERPFVEERTKUFUHUIUJABMUKABCCVCCVLNIEQULUMZV
      NUNUOUPUR $.
      $( [31-Mar-2014] $)

    $d x y I $.  $d x y P $.  $d x y X $.  $d y Y $.
    $( Group subtraction (division) operation. $)
    grpsubval $p |- ( ( X e. B /\ Y e. B ) ->
                    ( X M Y ) = ( X P ( I ` Y ) ) ) $=
      ( vx vy wcel wa co cv cfv cbs wceq cmpt2 cdm elfvdm eleq2s grpsubfval syl
      adantr oveqd oveq1 fveq2 oveq2d eqid ovex ovmpt2 eqtrd ) FANZGANZOZFGEPFG
      LMAALQZMQZDRZBPZUAZPFGDRZBPZUREVCFGURCSUBZNZEVCTUPVGUQVGFCSRAFCSUCHUDUGLM
      ABCDEVFHIJKUEUFUHLMFGAAVBVEVCFVABPUSFVABUIUTGTVAVDFBUTGDUJUKVCULFVDBUMUNU
      O $.
      $( [13-Dec-2014] $) $( [31-Mar-2014] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.
    grpinvcl.b $e |- B = ( Base ` G ) $.
    grpinvcl.n $e |- N = ( invg ` G ) $.
    $( A group element's inverse is a group element. $)
    grpinvcl $p |- ( ( G e. Grp /\ X e. B ) -> ( N ` X ) e. B ) $=
      ( vy cgrp wcel wa cfv cv cplusg co c0g wceq crio eqid grpinvval wreu syl
      grpinveu riotacl eqeltrd ) BHIDAIJZDCKGLDBMKZNBOKZPZGAQZAGAUFBCUGHDEUFRZU
      GRZFSUEUHGATUIAIGAUFBUGDEUJUKUBUHGAUCUAUD $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d w y B $.  $d y G $.  $d w y N $.  $d w y P $.  $d w y O $.  $d w y X $.
    grpinv.b $e |- B = ( Base ` G ) $.
    grpinv.p $e |- P = ( +g ` G ) $.
    grpinv.u $e |- O = ( 0g ` G ) $.
    grpinv.n $e |- N = ( invg ` G ) $.
    $( The properties of a group element's inverse. $)
    grpinv $p |- ( ( G e. Grp /\ X e. B ) ->
                 ( ( ( N ` X ) P X ) = O /\ ( X P ( N ` X ) ) = O ) ) $=
      ( vy vw cgrp wcel wa co wceq cv crio syl2anc wrex wreu grpidinv2 grpinveu
      cfv simprd wss wi wral ssid simpl rgenw riotass2 mpanl12 grpinvval eqtr4d
      wb grpinvcl reuss2 ax-17 a17d oveq1 eqeq1d oveq2 anbi12d riota2f mpbird )
      CMNFANOZFDUEZFBPZEQZFVIBPZEQZOZKRZFBPZEQZFVOBPZEQZOZKASZVIQZVHWAVQKASZVIV
      HVTKAUAZVQKAUBZWAWCQZVHEFBPFQFEBPFQOWDKABCEFGHIUCUFZKABCEFGHIUDZAAUGZVTVQ
      UHZKAUIZWDWEOZWFAUJZWJKAVQVSUKULZVTVQKAAUMUNTKABCDEMFGHIJUOUPVHVIANZVTKAU
      BZVNWBUQACDFGJURVHWDWEWPWGWHWIWKWLWPWMWNVTVQKAAUSUNTVTVNKLAVILRVINKUTWOVN
      KVAVOVIQZVQVKVSVMWQVPVJEVOVIFBVBVCWQVRVLEVOVIFBVDVCVEVFTVG $.
      $( [24-Aug-2011] $)

    $( The left inverse of a group element. $)
    grplinv $p |- ( ( G e. Grp /\ X e. B ) -> ( ( N ` X ) P X ) = O ) $=
      ( cgrp wcel wa cfv co wceq grpinv simpld ) CKLFALMFDNZFBOEPFSBOEPABCDEFGH
      IJQR $.
      $( [24-Aug-2011] $)

    $( The right inverse of a group element. $)
    grprinv $p |- ( ( G e. Grp /\ X e. B ) -> ( X P ( N ` X ) ) = O ) $=
      ( cgrp wcel wa cfv co wceq grpinv simprd ) CKLFALMFDNZFBOEPFSBOEPABCDEFGH
      IJQR $.
      $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid1 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( X P Y ) = O ) ) $=
      ( wcel w3a wceq co wa oveq2 adantl 3adant3 adantr cgrp cfv grprinv eqtr3d
      grplinv oveq1d grpinvcl adantrr simprl simprr grpass syldan 3impb 3adant2
      3jca grplid 3eqtr3d grprid 3eqtr3rd impbida ) CUALZFALZGALZMZFDUBZGNZFGBO
      ZENZVDVFPFVEBOZVGEVFVIVGNVDVEGFBQRVDVIENZVFVAVBVJVCABCDEFHIJKUCSTUDVDVHPV
      EVGBOZVEEBOZGVEVHVKVLNVDVGEVEBQRVDVKGNVHVDVEFBOZGBOZEGBOZVKGVAVBVNVONVCVA
      VBPVMEGBABCDEFHIJKUEUFSVAVBVCVNVKNZVAVBVCPZVEALZVBVCMVPVAVQPVRVBVCVAVBVRV
      CACDFHKUGZUHVAVBVCUIVAVBVCUJUOABCVEFGHIUKULUMVAVCVOGNVBABCEGHIJUPUNUQTVDV
      LVENZVHVAVBVTVCVAVBVRVTVSABCEVEHIJURULSTUSUT $.
      $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid2 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( Y P X ) = O ) ) $=
      ( wcel w3a wceq co wa oveq1 adantl 3adant3 adantr cgrp cfv grplinv eqtr3d
      grpinvcl grplid syldan eqcomd simprr simprl adantrr grpass grprinv oveq2d
      3jca 3impb grprid 3adant2 3eqtrd 3eqtr2d impbida ) CUALZFALZGALZMZFDUBZGN
      ZGFBOZENZVEVGPVFFBOZVHEVGVJVHNVEVFGFBQRVEVJENZVGVBVCVKVDABCDEFHIJKUCSTUDV
      EVIPVFEVFBOZVHVFBOZGVEVFVLNVIVEVLVFVBVCVLVFNZVDVBVCVFALZVNACDFHKUEZABCEVF
      HIJUFUGSUHTVIVMVLNVEVHEVFBQRVEVMGNVIVEVMGFVFBOZBOZGEBOZGVBVCVDVMVRNZVBVCV
      DPZVDVCVOMVTVBWAPVDVCVOVBVCVDUIVBVCVDUJVBVCVOVDVPUKUOABCGFVFHIULUGUPVBVCV
      RVSNVDVBVCPVQEGBABCDEFHIJKUMUNSVBVDVSGNVCABCEGHIJUQURUSTUTVA $.
      $( [24-Aug-2011] $)

    $d e w x B $.  $d e x G $.  $d e w x M $.  $d x N $.  $d e w x O $.
    $d e w x P $.
    $( Properties showing that a function ` M ` is the inverse function of a
       group. $)
    isgrpinv $p |- ( G e. Grp
  -> ( ( M : B --> B /\ A. x e. B ( ( M ` x ) P x ) = O ) <-> N = M ) ) $=
      ( ve cgrp wcel wf cv co wceq wa syl2anc vw cfv wral crio simplr grpinvval
      simplll simpr wreu wb simpllr ffvelrn grpinveu ax-17 oveq1 eqeq1d riota2f
      a17d mpbid eqtrd ralimdva impr wfn cmpt cvv eqid fnmpt riotaex grpinvfval
      ex a1i fneq1d adantr mpbiri ffn ad2antrl eqfnfv2 mpbird grpinvcl eqeltrrd
      mprg fmptd feq1d grplinv ralrimiva fveq1 oveq1d ralbidv anbi12d syl5ibcom
      jca feq1 impbid ) DMNZBBEOZAPZEUBZWPCQZGRZABUCZSZFERZWNXAXBWNXASZXBWPFUBZ
      WQRZABUCZWNWOWTXFWNWOSZWSXEABXGWPBNZSZWSXEXIWSSZXDLPZWPCQZGRZLBUDZWQXJWNX
      HXDXNRWNWOXHWSUGZXGXHWSUEZLBCDFGMWPHIJKUFZTXJWSXNWQRZXIWSUHXJWQBNZXMLBUIZ
      WSXRUJXJWOXHXSWNWOXHWSUKXPBBWPEULTXJWNXHXTXOXPLBCDGWPHIJUMTXMWSLUABWQUAPW
      QNLUNXSWSLURXKWQRXLWRGXKWQWPCUOUPUQTUSUTVJVAVBXCFBVCZEBVCZXBXFUJXCYAABXNV
      DZBVCZXNVENZYDABABXNYCVEYCVFZVGYEXHXMLBVHVKWAWNYAYDUJXAWNBFYCALBCDFGMHIJK
      VIZVLVMVNWOYBWNWTBBEVOVPABFEVQTVRVJWNBBFOZXDWPCQZGRZABUCZSXBXAWNYHYKWNYHB
      BYCOWNABXNBYCWNXHSXDXNBXQBDFWPHKVSVTYFWBWNBBFYCYGWCVRWNYJABBCDFGWPHIJKWDW
      EWKXBYHWOYKWTBBFEWLXBYJWSABXBYIWRGXBXDWQWPCWPFEWFWGUPWHWIWJWM $.
      $( [7-Aug-2013] $)
  $}

  ${
    grpinvid.u $e |- O = ( 0g ` G ) $.
    grpinvid.n $e |- N = ( invg ` G ) $.
    $( The inverse of the identity element of a group. $)
    grpinvid $p |- ( G e. Grp -> ( N ` O ) = O ) $=
      ( cgrp wcel cfv wceq cplusg co cbs eqid grpidcl grplid mpdan wb grpinvid1
      mpd3an23 mpbird ) AFGZCBHCIZCCAJHZKCIZUACALHZGZUDUEACUEMZDNZUEUCACCUGUCMZ
      DOPUAUFUFUBUDQUHUHUEUCABCCCUGUIDERST $.
      $( [24-Aug-2011] $)
  $}

  ${
    grplcan.b $e |- B = ( Base ` G ) $.
    grplcan.p $e |- P = ( +g ` G ) $.
    $( Left cancellation law for groups. $)
    grplcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( Z P X ) = ( Z P Y ) <-> X = Y ) ) $=
      ( wcel w3a wa co wceq cfv oveq2 eqid adantlr adantrl adantr 3eqtr3d exp53
      cgrp cminusg adantl c0g grplinv oveq1d grpinvcl simprr simprl 3jca grpass
      wi syldan anassrs grplid adantrr 3imp2 impbid1 ) CUBIZDAIZEAIZFAIZJKFDBLZ
      FEBLZMZDEMZUTVAVBVCVFVGUMUTVAVBVCVFVGUTVAKZVBVCKZKZVFKFCUCNZNZVDBLZVLVEBL
      ZDEVFVMVNMVJVDVEVLBOUDVJVMDMZVFVHVCVOVBVHVCKZVLFBLZDBLZCUENZDBLZVMDVPVQVS
      DBUTVCVQVSMZVAABCVKVSFGHVSPZVKPZUFZQUGUTVAVCVRVMMZUTVAVCKZVLAIZVCVAJWEUTW
      FKWGVCVAUTVCWGVAACVKFGWCUHZRUTVAVCUIUTVAVCUJUKABCVLFDGHULUNUOVHVTDMVCABCV
      SDGHWBUPSTRSVJVNEMZVFUTVIWIVAUTVIKZVQEBLZVSEBLZVNEWJVQVSEBUTVCWAVBWDRUGUT
      VIWGVCVBJWKVNMWJWGVCVBUTVCWGVBWHRUTVBVCUIUTVBVCUJUKABCVLFEGHULUNUTVBWLEMV
      CABCVSEGHWBUPUQTQSTUAURDEFBOUS $.
      $( [25-Aug-2011] $)
  $}

  ${
    grpinvinv.b $e |- B = ( Base ` G ) $.
    grpinvinv.n $e |- N = ( invg ` G ) $.
    $( Double inverse law for groups.  Lemma 2.2.1(c) of [Herstein] p. 55. $)
    grpinvinv $p |- ( ( G e. Grp /\ X e. B ) -> ( N ` ( N ` X ) ) = X ) $=
      ( cgrp wcel wa cfv cplusg co wceq c0g grpinvcl eqid grprinv syldan eqtr4d
      grplinv wb simpl simpr grplcan syl13anc mpbid ) BGHZDAHZIZDCJZUJCJZBKJZLZ
      UJDULLZMZUKDMZUIUMBNJZUNUGUHUJAHZUMUQMABCDEFOZAULBCUQUJEULPZUQPZFQRAULBCU
      QDEUTVAFTSUIUGUKAHZUHURUOUPUAUGUHUBUGUHURVBUSABCUJEFORUGUHUCUSAULBUKDUJEU
      TUDUEUF $.
      $( [31-Mar-2014] $)

    $d x y B $.  $d x y G $.  $d x y N $.  $d x y ph $.
    grpinvf1o.g $e |- ( ph -> G e. Grp ) $.
    $( The group inverse is a one-to-one onto function. $)
    grpinvf1o $p |- ( ph -> N : B -1-1-onto-> B ) $=
      ( vx vy wfn wceq cv cfv wral cvv wcel eqid cgrp wa sylan crn wi cplusg co
      weq wf1o c0g crio cmpt riotaex rgenw fnmpt ax-mp grpinvfval fneq1d mpbiri
      wrex wb fvelrnb grpinvcl eleq1 syl5ibcom rexlimdva grpinvinv fveq2 eqeq1d
      syl rcla4ev syl2anc impbid bitrd eqrdv adantrr adantrl eqeq12d ralrimivva
      ex syl5ib dff1o6 syl3anbrc ) ADBJZDUAZBKHLZDMZILZDMZKZHIUEZUBZIBNHBNBBDUF
      AWAHBWEWCCUCMZUDCUGMZKZIBUHZUIZBJZWMOPZHBNWOWPHBWLIBUJUKHBWMWNOWNQULUMABD
      WNACRPZDWNKGHIBWJCDWKREWJQWKQFUNVGUOUPZAIWBBAWEWBPZWDWEKZHBUQZWEBPZAWAWSX
      AURWRHBWEDUSVGAXAXBAWTXBHBAWCBPZSWDBPZWTXBAWQXCXDGBCDWCEFUTTWDWEBVAVBVCAX
      BXAAWQXBXAGWQXBSWFBPWFDMZWEKZXABCDWEEFUTBCDWEEFVDZWTXFHWFBWCWFKWDXEWEWCWF
      DVEVFVHVITVQVJVKVLAWIHIBBWGWDDMZXEKAXCXBSSZWHWDWFDVEXIXHWCXEWEAXCXHWCKZXB
      AWQXCXJGBCDWCEFVDTVMAXBXFXCAWQXBXFGXGTVNVOVRVPHIBBDVSVT $.
      $( [22-Oct-2014] $)
  $}

  ${
    $d w x y z B $.  $d w x y z K $.  $d w x y z L $.  $d w x y z ph $.
    grpidpropd.1 $e |- ( ph -> B = ( Base ` K ) ) $.
    grpidpropd.2 $e |- ( ph -> B = ( Base ` L ) ) $.
    grpidpropd.3 $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
      ( x ( +g ` K ) y ) = ( x ( +g ` L ) y ) ) $.
    grpidpropd.4 $e |- ( ph -> K e. _V ) $.
    grpidpropd.5 $e |- ( ph -> L e. _V ) $.
    $( If two structures have the same group components (properties), they have
       the same identity element. $)
    grpidpropd $p |- ( ph -> ( 0g ` K ) = ( 0g ` L ) ) $=
      ( cv cfv co wceq wa wral crio wcel eqid vz cplusg cbs c0g proplem ancom2s
      vw eqeq1d anbi12d anassrs ralbidva riotabidva raleqdv riotaeqbidv 3eqtr3d
      wb cvv grpidval syl 3eqtr4d ) ABLZCLZEUBMZNZVBOZVBVAVCNZVBOZPZCEUCMZQZBVI
      RZVAVBFUBMZNZVBOZVBVAVLNZVBOZPZCFUCMZQZBVRRZEUDMZFUDMZAVHCDQZBDRVQCDQZBDR
      VKVTAWCWDBDAVADSZPVHVQCDAWEVBDSZVHVQUPAWEWFPPZVEVNVGVPWGVDVMVBIUHWGVFVOVB
      AWFWEVFVOOAUAUGDDVCVLVBVAABCDDVCVLUALUGLIUEUEUFUHUIUJUKULAWCVJBDVIGAVHCDV
      IGUMUNAWDVSBDVRHAVQCDVRHUMUNUOAEUQSWAVKOJCVIVCBEWAUQVITVCTWATURUSAFUQSWBV
      TOKCVRVLBFWBUQVRTVLTWBTURUSUT $.
      $( [27-Nov-2014] $)

    $( If two structures have the same group components (properties), they have
       the same group inversion function. $)
    grpinvpropd $p |- ( ph -> ( invg ` K ) = ( invg ` L ) ) $=
      ( cbs cfv wceq crio cmpt wcel wa cvv eqid cv cplusg co cminusg grpidpropd
      c0g adantr eqeq12d anassrs an32s riotabidva mpteq2dva riotaeqdv mpteq12dv
      wb 3eqtr3d grpinvfval syl 3eqtr4d ) ACELMZBUAZCUAZEUBMZUCZEUFMZNZBUTOZPZC
      FLMZVAVBFUBMZUCZFUFMZNZBVIOZPZEUDMZFUDMZACDVFBDOZPCDVMBDOZPVHVOACDVRVSAVB
      DQZRVFVMBDAVADQZVTVFVMUOZAWAVTWBAWAVTRZRVDVKVEVLIAVEVLNWCABCDEFGHIJKUEUGU
      HUIUJUKULACDVRUTVGGAVFBDUTGUMUNACDVSVIVNHAVMBDVIHUMUNUPAESQVPVHNJCBUTVCEV
      PVESUTTVCTVETVPTUQURAFSQVQVONKCBVIVJFVQVLSVITVJTVLTVQTUQURUS $.
      $( [27-Nov-2014] $)
  $}

  ${
    grpinvadd.b $e |- B = ( Base ` G ) $.
    grpinvadd.p $e |- P = ( +g ` G ) $.
    grpinvadd.n $e |- N = ( invg ` G ) $.
    $( The inverse of the group operation reverses the arguments.  Lemma
       2.2.1(d) of [Herstein] p. 55. $)
    grpinvadd $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                   ( N ` ( X P Y ) ) = ( ( N ` Y ) P ( N ` X ) ) ) $=
      ( wcel co cfv wceq grpinvcl 3adant2 3adant3 grpcl syl3anc grpass syl13anc
      cgrp w3a c0g simp1 simp2 simp3 eqid grprinv oveq1d grplid syl2anc 3eqtr3d
      oveq2d 3eqtrd wb grpinvid1 mpbird ) CUAJZEAJZFAJZUBZEFBKZDLFDLZEDLZBKZMZV
      BVEBKZCUCLZMZVAVGEFVEBKZBKZEVDBKZVHVAURUSUTVEAJZVGVKMURUSUTUDZURUSUTUEURU
      SUTUFZVAURVCAJZVDAJZVMVNURUTVPUSACDFGINOZURUSVQUTACDEGINPZABCVCVDGHQRZABC
      EFVEGHSTVAVJVDEBVAFVCBKZVDBKZVHVDBKZVJVDVAWAVHVDBURUTWAVHMUSABCDVHFGHVHUG
      ZIUHOUIVAURUTVPVQWBVJMVNVOVRVSABCFVCVDGHSTVAURVQWCVDMVNVSABCVHVDGHWDUJUKU
      LUMURUSVLVHMUTABCDVHEGHWDIUHPUNVAURVBAJVMVFVIUOVNABCEFGHQVTABCDVHVBVEGHWD
      IUPRUQ $.
      $( [27-Oct-2006] $)
  $}

  ${
    $d x y B $.  $d x y G $.
    grpsubcl.b $e |- B = ( Base ` G ) $.
    grpsubcl.m $e |- M = ( -g ` G ) $.
    $( Functionality of group subtraction. $)
    grpsubf $p |- ( G e. Grp -> M : ( B X. B ) --> B ) $=
      ( vx vy cgrp wcel cxp wf cv cminusg cfv cplusg cmpt2 wral eqid grpinvcl
      co 3adant2 grpcl syld3an3 3expb ralrimivva fmpt2 sylib grpsubfval mpbird
      feq1d ) BHIZAAJZACKULAFGAAFLZGLZBMNZNZBONZTZPZKZUKURAIZGAQFAQUTUKVAFGAAUK
      UMAIZUNAIZVAUKVBVCUPAIZVAUKVCVDVBABUOUNDUORZSUAAUQBUMUPDUQRZUBUCUDUEFGAAU
      RAUSUSRUFUGUKULACUSFGAUQBUOCHDVFVEEUHUJUI $.
      $( [9-Sep-2014] $)

    $( Closure of group subtraction. $)
    grpsubcl $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) -> ( X M Y ) e. B ) $=
      ( cgrp wcel cxp wf co grpsubf fovrn syl3an1 ) BHIAAJACKDAIEAIDECLAIABCFGM
      DEAAACNO $.
      $( [31-Mar-2014] $)

    $( Right cancellation law for group subtraction. $)
    grpsubrcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( X M Z ) = ( Y M Z ) <-> X = Y ) ) $=
      ( cgrp wcel w3a wa co wceq cminusg cfv cplusg wb eqid grpsubval 3ad2antr3
      3adant2 3adant1 eqeq12d adantl simpl simpr1 simpr2 grpinvcl grprcan bitrd
      syl13anc ) BIJZDAJZEAJZFAJZKZLZDFCMZEFCMZNZDFBOPZPZBQPZMZEVCVDMZNZDENZUQV
      AVGRUMUQUSVEUTVFUNUPUSVENUOAVDBVBCDFGVDSZVBSZHTUBUOUPUTVFNUNAVDBVBCEFGVIV
      JHTUCUDUEURUMUNUOVCAJZVGVHRUMUQUFUMUNUOUPUGUMUNUOUPUHUMUNUPVKUOABVBFGVJUI
      UAAVDBDEVCGVIUJULUK $.
      $( [31-Mar-2014] $)

    grpsubinv.m $e |- N = ( invg ` G ) $.
    $( Inverse of a group subtraction. $)
    grpsubinv $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                      ( N ` ( X M Y ) ) = ( Y M X ) ) $=
      ( cgrp wcel w3a cfv cplusg co wceq grpinvcl 3adant2 grpsubval 3adant1
      eqid grpinvadd syld3an3 grpinvinv oveq1d eqtrd fveq2d ancoms 3eqtr4d ) BJ
      KZEAKZFAKZLZEFDMZBNMZOZDMZFEDMZUOOZEFCOZDMFECOZUMUQUNDMZURUOOZUSUJUKULUNA
      KZUQVCPUJULVDUKABDFGIQRAUOBDEUNGUOUAZIUBUCUMVBFURUOUJULVBFPUKABDFGIUDRUEU
      FUMUTUPDUKULUTUPPUJAUOBDCEFGVEIHSTUGUKULVAUSPZUJULUKVFAUOBDCFEGVEIHSUHTUI
      $.
      $( [9-Sep-2014] $)
  $}

  ${
    grpsubid.b $e |- B = ( Base ` G ) $.
    grpsubid.o $e |- O = ( 0g ` G ) $.
    grpsubid.m $e |- M = ( -g ` G ) $.
    $( Subtraction of a group element from itself. $)
    grpsubid $p |- ( ( G e. Grp /\ X e. B ) -> ( X M X ) = O ) $=
      ( cgrp wcel wa co cminusg cfv cplusg wceq eqid grpsubval anidms adantl
      grprinv eqtrd ) BIJZEAJZKEECLZEEBMNZNBONZLZDUDUEUHPZUCUDUIAUGBUFCEEFUGQZU
      FQZHRSTAUGBUFDEFUJGUKUAUB $.
      $( [31-Mar-2014] $)

    $( If the difference between two group elements is zero, they are equal.
       ( ~ subeq0 analog.) $)
    grpsubeq0 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B )
                  -> ( ( X M Y ) = O <-> X = Y ) ) $=
      ( cgrp wcel w3a co wceq cminusg cfv cplusg eqid eqeq1d 3adant2 3adant1 wb
      grpsubval simp1 grpinvcl simp2 grpinvid2 syl3anc grpinvinv syl6bb 3bitr2d
      eqcom ) BJKZEAKZFAKZLZEFCMZDNEFBOPZPZBQPZMZDNZUSURPZENZEFNZUPUQVADUNUOUQV
      ANUMAUTBURCEFGUTRZURRZIUCUASUPUMUSAKZUNVDVBUBUMUNUOUDUMUOVHUNABURFGVGUETU
      MUNUOUFAUTBURDUSEGVFHVGUGUHUPVDFENVEUPVCFEUMUOVCFNUNABURFGVGUITSFEULUJUK
      $.
      $( [31-Mar-2014] $)
  $}

  ${
    grpsubadd.b $e |- B = ( Base ` G ) $.
    grpsubadd.p $e |- P = ( +g ` G ) $.
    grpsubadd.m $e |- M = ( -g ` G ) $.
    $( Relationship between group subtraction and addition. $)
    grpsubadd $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) )
          -> ( ( X M Y ) = Z <-> ( Z P Y ) = X ) ) $=
      ( cgrp wcel w3a co wceq cfv eqid eqeq1d 3ad2antr2 syl13anc cminusg adantl
      wa grpsubval 3adant3 wb simpl simpr1 grpinvcl grpcl syl3anc simpr3 simpr2
      grprcan c0g grpass grplinv oveq2d grprid 3ad2antr1 3eqtrd 3bitr2d syl6bb
      eqcom ) CKLZEALZFALZGALZMZUCZEFDNZGOZEGFBNZOZVMEOVJVLEFCUAPZPZBNZGOZVQFBN
      ZVMOZVNVJVKVQGVIVKVQOZVEVFVGWAVHABCVODEFHIVOQZJUDUEUBRVJVEVQALZVHVGVTVRUF
      VEVIUGZVJVEVFVPALZWCWDVEVFVGVHUHZVEVFVGWEVHACVOFHWBUISZABCEVPHIUJUKVEVFVG
      VHULVEVFVGVHUMZABCVQGFHIUNTVJVSEVMVJVSEVPFBNZBNZECUOPZBNZEVJVEVFWEVGVSWJO
      WDWFWGWHABCEVPFHIUPTVJWIWKEBVEVFVGWIWKOVHABCVOWKFHIWKQZWBUQSURVEVGVFWLEOV
      HABCWKEHIWMUSUTVARVBEVMVDVC $.
      $( [31-Mar-2014] $)

    $( Double group subtraction. $)
    grpsubsub $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( X M ( Y M Z ) ) = ( X P ( Z M Y ) ) ) $=
      ( cgrp wcel w3a wa co cminusg cfv wceq simpr1 3adant3r1 grpsubval syl2anc
      grpsubcl eqid grpsubinv oveq2d eqtrd ) CKLZEALZFALZGALZMNZEFGDOZDOZEUMCPQ
      ZQZBOZEGFDOZBOULUIUMALZUNUQRUHUIUJUKSUHUJUKUSUIACDFGHJUCTABCUODEUMHIUOUDZ
      JUAUBULUPUREBUHUJUKUPURRUIACDUOFGHJUTUETUFUG $.
      $( [2-Dec-2014] $) $( [24-Feb-2008] $)

    $( Associative-type law for group subtraction and addition. $)
    grpaddsubass $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X P Y ) M Z ) = ( X P ( Y M Z ) ) ) $=
      ( cgrp wcel w3a wa co cminusg cfv wceq grpsubval syl2anc simpr1 3ad2antr3
      simpl simpr2 eqid grpinvcl grpass syl13anc grpcl 3adant3r3 simpr3 3eqtr4d
      oveq2d ) CKLZEALZFALZGALZMZNZEFBOZGCPQZQZBOZEFVBBOZBOZUTGDOZEFGDOZBOUSUNU
      OUPVBALZVCVERUNURUCUNUOUPUQUAUNUOUPUQUDZUNUOUQVHUPACVAGHVAUEZUFUBABCEFVBH
      IUGUHUSUTALZUQVFVCRUNUOUPVKUQABCEFHIUIUJUNUOUPUQUKZABCVADUTGHIVJJSTUSVGVD
      EBUSUPUQVGVDRVIVLABCVADFGHIVJJSTUMUL $.
      $( [16-Apr-2014] $)

    $( Cancellation law for subtraction ( ~ pncan analog). $)
    grppncan $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
        ( ( X P Y ) M Y ) = X ) $=
      ( cgrp wcel w3a co c0g cfv wceq simp1 simp2 simp3 grpaddsubass wa 3adant2
      syl13anc eqid grpsubid oveq2d grprid 3adant3 3eqtrd ) CJKZEAKZFAKZLZEFBMF
      DMZEFFDMZBMZECNOZBMZEUMUJUKULULUNUPPUJUKULQUJUKULRUJUKULSZUSABCDEFFGHITUC
      UJULUPURPUKUJULUAUOUQEBACDUQFGUQUDZIUEUFUBUJUKUREPULABCUQEGHUTUGUHUI $.
      $( [16-Apr-2014] $)

    $( Cancellation law for subtraction ( ~ npcan analog). . $)
    grpnpcan $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
        ( ( X M Y ) P Y ) = X ) $=
      ( cgrp wcel w3a cminusg cfv co wceq eqid 3adant2 syld3an3 grpsubval grpcl
      grpinvcl syl2anc grppncan 3adant1 eqcomd grpinvinv oveq12d 3eqtr3rd ) CJK
      ZEAKZFAKZLZEFCMNZNZBOZUODOZUPUOUNNZBOZEEFDOZFBOUMUPAKZUOAKZUQUSPUJUKULVBV
      AUJULVBUKACUNFGUNQZUBRZABCEUOGHUASVDABCUNDUPUOGHVCITUCUJUKULVBUQEPVDABCDE
      UOGHIUDSUMUPUTURFBUMUTUPUKULUTUPPUJABCUNDEFGHVCITUEUFUJULURFPUKACUNFGVCUG
      RUHUI $.
      $( [19-Apr-2014] $)

    $( Double group subtraction ( ~ subsub4 analog).  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    grpsubsub4 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X M Y ) M Z ) = ( X M ( Z P Y ) ) ) $=
      ( cgrp wcel w3a wa co wceq grpsubcl grpnpcan syl3anc syl13anc simpl grpcl
      3adant3r3 simpr3 oveq1d simpr2 grpass simpr1 3eqtr3d wb grpsubadd mpbird
      eqcomd ) CKLZEALZFALZGALZMZNZEGFBOZDOZEFDOZGDOZUSVAVCPZVCUTBOZEPZUSVCGBOZ
      FBOZVBFBOZVEEUSVGVBFBUSUNVBALZUQVGVBPUNURUAZUNUOUPVJUQACDEFHJQUCZUNUOUPUQ
      UDZABCDVBGHIJRSUEUSUNVCALZUQUPVHVEPVKUSUNVJUQVNVKVLVMACDVBGHJQSZVMUNUOUPU
      QUFZABCVCGFHIUGTUSUNUOUPVIEPVKUNUOUPUQUHZVPABCDEFHIJRSUIUSUNUOUTALZVNVDVF
      UJVKVQUSUNUQUPVRVKVMVPABCGFHIUBSVOABCDEUTVCHIJUKTULUM $.
      $( [2-Dec-2014] $)

    $( Cancellation law for mixed addition and subtraction.  ( ~ pnpcan2
       analog.) $)
    grppnpcan2 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X P Z ) M ( Y P Z ) ) = ( X M Y ) ) $=
      ( cgrp wcel w3a wa co wceq simpl grpcl 3adant3r2 simpr3 simpr2 grpsubsub4
      syl13anc grppncan oveq1d eqtr3d ) CKLZEALZFALZGALZMZNZEGBOZGDOZFDOZUMFGBO
      DOZEFDOULUGUMALZUJUIUOUPPUGUKQUGUHUJUQUIABCEGHIRSUGUHUIUJTUGUHUIUJUAABCDU
      MGFHIJUBUCULUNEFDUGUHUJUNEPUIABCDEGHIJUDSUEUF $.
      $( [2-Dec-2014] $) $( [15-Feb-2008] $)

    $( Cancellation law for group subtraction.  ( ~ npncan analog.) $)
    grpnpncan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X M Y ) P ( Y M Z ) ) = ( X M Z ) ) $=
      ( cgrp wcel w3a wa co wceq simpl grpsubcl 3adant3r3 simpr2 simpr3 oveq1d
      grpaddsubass syl13anc grpnpcan eqtr3d ) CKLZEALZFALZGALZMZNZEFDOZFBOZGDOZ
      UMFGDOBOZEGDOULUGUMALZUIUJUOUPPUGUKQUGUHUIUQUJACDEFHJRSUGUHUIUJTUGUHUIUJU
      AABCDUMFGHIJUCUDULUNEGDUGUHUIUNEPUJABCDEFHIJUESUBUF $.
      $( [2-Dec-2014] $) $( [15-Feb-2008] $)
  $}

  ${
    grpnnncan2.b $e |- B = ( Base ` G ) $.
    grpnnncan2.m $e |- M = ( -g ` G ) $.
    $( Cancellation law for group subtraction.  ( ~ nnncan2 analog.) $)
    grpnnncan2 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X M Z ) M ( Y M Z ) ) = ( X M Y ) ) $=
      ( cgrp wcel w3a wa co cplusg cfv wceq simpl simpr1 simpr3 3adant3r1 eqtrd
      grpsubcl eqid grpsubsub4 syl13anc grpnpcan oveq2d ) BIJZDAJZEAJZFAJZKZLZD
      FCMEFCMZCMZDUNFBNOZMZCMZDECMUMUHUIUKUNAJZUOURPUHULQUHUIUJUKRUHUIUJUKSUHUJ
      UKUSUIABCEFGHUBTAUPBCDFUNGUPUCZHUDUEUMUQEDCUHUJUKUQEPUIAUPBCEFGUTHUFTUGUA
      $.
      $( [2-Dec-2014] $) $( [15-Feb-2008] $)
  $}

  ${
    $d n w x B $.  $d n s w x G $.  $d n s w x I $.  $d n x N $.  $d n w x O $.
    $d s w P $.  $d n x S $.  $d n x X $.
    mulgval.b $e |- B = ( Base ` G ) $.
    mulgval.p $e |- P = ( +g ` G ) $.
    mulgval.o $e |- O = ( 0g ` G ) $.
    mulgval.i $e |- I = ( invg ` G ) $.
    mulgval.t $e |- T = ( .g ` G ) $.
    $( Group multiple (exponentiation) operation. $)
    mulgfval $p |- ( G e. V -> T = ( n e. ZZ , x e. B |->
      if ( n = 0 , O , if ( 0 < n ,
        ( seq 1 ( P , ( NN X. { x } ) ) ` n ) ,
        ( I ` ( seq 1 ( P , ( NN X. { x } ) ) ` -u n ) ) ) ) ) ) $=
      ( vs cvv cz cv wceq cfv vw wcel cc0 clt wbr cn csn cxp c1 cseq cneg cmpt2
      cif cmg cbs c0g cplusg cminusg csb eqidd fveq2 syl6eqr biidd seqex a1i wa
      elex seqeq2d sylan9eqr fveq1d fveq2d fveq12d ifbieq12d csbied mpt2eq123dv
      id simpl df-mulg zex fvex eqeltri mpt2ex fvmpt syl5eq syl ) FIUBFPUBZDEAQ
      BERZUCSZHUCWGUDUEZWGCUFARUGUHZUIUJZTZWGUKZWKTZGTZUMZUMZULZSFIVGWFDFUNTWRN
      UAFEAQUARZUOTZWHWSUPTZOWSUQTZWJUIUJZWIWGORZTZWMXDTZWSURTZTZUMZUSZUMZULWRP
      UNWSFSZEAQWTXKQBWQXLQUTXLWTFUOTZBWSFUOVAJVBXLWHWHXAXJHWPXLWHVCXLXAFUPTHWS
      FUPVALVBXLOXCXIWPPXCPUBXLXBWJUIVDVEXLXDXCSZVFZWIWIXEXHWLWOXOWIVCXOWGXDWKX
      NXLXDXCWKXNVPXLXBCWJUIXLXBFUQTCWSFUQVAKVBVHVIZVJXOXFWNXGGXOXGFURTGXOWSFUR
      XLXNVQVKMVBXOWMXDWKXPVJVLVMVNVMVOAUAEOVREAQBWQVSBXMPJFUOVTWAWBWCWDWE $.
      $( [11-Dec-2014] $)

    mulgval.s $e |- S = seq 1 ( P , ( NN X. { X } ) ) $.
    $( Group multiple (exponentiation) operation. $)
    mulgval $p |- ( ( N e. ZZ /\ X e. B ) -> ( N T X ) =
      if ( N = 0 , O , if ( 0 < N , ( S ` N ) , ( I ` ( S ` -u N ) ) ) ) ) $=
      ( vn vx cc0 wceq cfv cz wcel wa co cv clt wbr cn csn cxp c1 cseq cneg cif
      cmpt2 cbs cdm elfvdm eleq2s adantl mulgfval oveqd syl simpl eqeq1d breq2d
      simpr sneqd xpeq2d seqeq3d syl6eqr fveq12d negeqd ifbieq12d ifbieq2d eqid
      fveq2d c0g cvv fvex eqeltri ifex ovmpt2a eqtrd ) GUAUBZIAUBZUCZGIDUDZGIPQ
      UAAPUEZRSZHRWIUFUGZWIBUHQUEZUIZUJZUKULZTZWIUMZWOTZFTZUNZUNZUOZUDZGRSZHRGU
      FUGZGCTZGUMZCTZFTZUNZUNZWGEUPUQZUBZWHXCSWFXMWEXMIEUPTAIEUPURJUSUTXMDXBGIQ
      ABDPEFHXLJKLMNVAVBVCPQGIUAAXAXKXBWIGSZWLISZUCZWJXDWTXJHXPWIGRXNXOVDZVEXPW
      KXEWPWSXFXIXPWIGRUFXQVFXPWIGWOCXPWOBUHIUIZUJZUKULCXPWNXSBUKXPWMXRUHXPWLIX
      NXOVGVHVIVJOVKZXQVLXPWRXHFXPWQXGWOCXTXPWIGXQVMVLVQVNVOXBVPXDHXJHEVRTVSLEV
      RVTWAXEXFXIGCVTXHFVTWBWBWCWD $.
      $( [11-Dec-2014] $)
  $}

  ${
    mulg0.b $e |- B = ( Base ` G ) $.
    mulg0.o $e |- O = ( 0g ` G ) $.
    mulg0.t $e |- T = ( .g ` G ) $.
    $( Group multiple (exponentiation) operation at zero. $)
    mulg0 $p |- ( X e. B -> ( 0 T X ) = O ) $=
      ( cc0 cz wcel co wceq 0z wa clt wbr cfv cif eqid cplusg csn cxp cseq cneg
      cn c1 cminusg mulgval iftrue ax-mp syl6eq mpan ) IJKZEAKZIEBLZDMNUNUOOUPI
      IMZDIIPQICUARZUFEUBUCUGUDZRIUEUSRCUHRZRSZSZDAURUSBCUTIDEFURTGUTTHUSTUIUQV
      BDMITUQDVAUJUKULUM $.
      $( [11-Dec-2014] $)
  $}

  ${
    mulgnn.b $e |- B = ( Base ` G ) $.
    mulgnn.p $e |- P = ( +g ` G ) $.
    mulgnn.t $e |- T = ( .g ` G ) $.
    mulgnn.s $e |- S = seq 1 ( P , ( NN X. { X } ) ) $.
    $( Group multiple (exponentiation) operation at a positive integer. $)
    mulgnn $p |- ( ( N e. NN /\ X e. B ) -> ( N T X ) = ( S ` N ) ) $=
      ( cn wcel cc0 wceq cfv cif eqid syl eqtrd wa c0g clt wbr cneg cminusg nnz
      co cz mulgval sylan wn wne nnne0 df-ne sylib iffalse nngt0 iftrue adantr
      ) FLMZGAMZUAFGDUHZFNOZEUBPZNFUCUDZFCPZFUECPEUFPZPZQZQZVGVAFUIMVBVCVKOFUGA
      BCDEVHFVEGHIVERVHRJKUJUKVAVKVGOVBVAVKVJVGVAVDULZVKVJOVAFNUMVLFUNFNUOUPVDV
      EVJUQSVAVFVJVGOFURVFVGVIUSSTUTT $.
      $( [11-Dec-2014] $)
  $}

  ${
    mulg1.b $e |- B = ( Base ` G ) $.
    mulg1.m $e |- T = ( .g ` G ) $.
    $( Group multiple (exponentiation) operation at one. $)
    mulg1 $p |- ( X e. B -> ( 1 T X ) = X ) $=
      ( wcel c1 co cplusg cfv cn csn cxp cseq wceq 1nn eqid mulgnn mpan mpan2
      1z fvconst2g seq1i eqtrd ) DAGZHDBIZHCJKZLDMNZHOZKZDHLGZUFUGUKPQAUHUJBCHD
      EUHRFUJRSTUFDUHUIHUBUFULHUIKDPQLDHAUCUAUDUE $.
      $( [11-Dec-2014] $)

    ${
      mulgnnp1.p $e |- P = ( +g ` G ) $.
      $( Group multiple (exponentiation) operation at a successor. $)
      mulgnnp1 $p |- ( ( N e. NN /\ X e. B ) ->
        ( ( N + 1 ) T X ) = ( ( N T X ) P X ) ) $=
        ( cn wcel wa c1 caddc co csn cxp cfv wceq mulgnn cseq cuz nnuz syl6eleq
        simpl seqp1 syl id peano2nn fvconst2g syl2anr oveq2d eqtrd sylan oveq1d
        eqid 3eqtr4d ) EJKZFAKZLZEMNOZBJFPQZMUAZRZEVCRZFBOZVAFCOZEFCOZFBOUTVDVE
        VAVBRZBOZVFUTEMUBRZKVDVJSUTEJVKURUSUEUCUDBVBMEUFUGUTVIFVEBUSUSVAJKZVIFS
        URUSUHEUIZJFVAAUJUKULUMURVLUSVGVDSVMABVCCDVAFGIHVCUPZTUNUTVHVEFBABVCCDE
        FGIHVNTUOUQ $.
        $( [11-Dec-2014] $)
    $}

    mulgnegnn.i $e |- I = ( invg ` G ) $.
    $( Group multiple (exponentiation) operation at a negative integer. $)
    mulgnegnn $p |- ( ( N e. NN /\ X e. B ) ->
      ( -u N T X ) = ( I ` ( N T X ) ) ) $=
      ( cn wcel cfv wceq syl fveq2d cc0 clt wbr eqid cr wa cneg cplusg csn cseq
      cxp c1 co cc nncn negneg adantr c0g cif cz nnnegz mulgval sylan wne nnne0
      wn wb negeq0 necon3abid mpbid iffalse renegcl nngt0 lt0neg2 wi 0re ltnsym
      nnre mpan2 sylc eqtrd mulgnn 3eqtr4d ) EJKZFAKZUAZEUBZUBZCUCLZJFUDUFUGUEZ
      LZDLZEWELZDLWBFBUHZEFBUHZDLWAWFWHDWAWCEWEVSWCEMZVTVSEUIKZWKEUJZEUKNULOOWA
      WIWBPMZCUMLZPWBQRZWBWELZWGUNZUNZWGVSWBUOKVTWIWSMEUPAWDWEBCDWBWOFGWDSZWOSI
      HWESZUQURVSWSWGMVTVSWSWRWGVSWNVAZWSWRMVSEPUSZXBEUTVSWLXCXBVBWMWLWNEPEVCVD
      NVEWNWOWRVFNVSWPVAZWRWGMVSWBTKZWBPQRZXDVSETKZXEEVMZEVGNVSPEQRZXFEVHVSXGXI
      XFVBXHEVINVEXEPTKXFXDVJVKWBPVLVNVOWPWQWGVFNVPULVPWAWJWHDAWDWEBCEFGWTHXAVQ
      OVR $.
      $( [11-Dec-2014] $)
  $}

  ${
    mulgnn0p1.b $e |- B = ( Base ` G ) $.
    mulgnn0p1.t $e |- T = ( .g ` G ) $.
    mulgnn0p1.p $e |- P = ( +g ` G ) $.
    $( Group multiple (exponentiation) operation at a successor, extended to
       ` NN0 ` .  TODO:  This can be generalized to a monoid if/when we
       introduce them. $)
    mulgnn0p1 $p |- ( ( G e. Grp /\ N e. NN0 /\ X e. B ) ->
      ( ( N + 1 ) T X ) = ( ( N T X ) P X ) ) $=
      ( cgrp wcel cc0 wceq c1 caddc co wa adantl oveq1d oveq1 cn wo simp2 elnn0
      cn0 w3a sylib simpr simpl3 mulgnnp1 syl2anc c0g cfv grplid mulg0 3eqtr4rd
      eqid mulg1 3adant2 0p1e1 syl6eqr eqeq12d syl5ibrcom imp jaodan mpdan ) DJ
      KZEUEKZFAKZUFZEUAKZELMZUBZENOPZFCPZEFCPZFBPZMZVJVHVMVGVHVIUCEUDUGVJVKVRVL
      VJVKQVKVIVRVJVKUHVGVHVIVKUIABCDEFGHIUJUKVJVLVRVJVRVLNFCPZLFCPZFBPZMZVGVIW
      BVHVGVIQZDULUMZFBPFWAVSABDWDFGIWDUQZUNWCVTWDFBVIVTWDMVGACDWDFGWEHUORSVIVS
      FMVGACDFGHURRUPUSVLVOVSVQWAVLVNNFCVLVNLNOPNELNOTUTVASVLVPVTFBELFCTSVBVCVD
      VEVF $.
      $( [11-Dec-2014] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d x y N $.  $d x y X $.
    mulgnncl.b $e |- B = ( Base ` G ) $.
    mulgnncl.t $e |- T = ( .g ` G ) $.
    $( Closure of the group multiple (exponentiation) operation.  TODO:  This
       can be generalized to a magma if/when we introduce them. $)
    mulgnncl $p |- ( ( G e. Grp /\ N e. NN /\ X e. B ) -> ( N T X ) e. B ) $=
      ( vx vy cgrp wcel cn co cfv c1 wceq eqid cv wa eqeltrd w3a cplusg csn cxp
      cseq mulgnn 3adant1 cuz simp2 nnuz syl6eleq simp3 elfznn fvconst2g syl2an
      cfz simpl3 grpcl 3expb 3ad2antl1 seqcl ) CJKZDLKZEAKZUAZDEBMZDCUBNZLEUCUD
      ZOUEZNZAVCVDVFVJPVBAVGVIBCDEFVGQZGVIQUFUGVEHIVGAVHODVEDLOUHNVBVCVDUIUJUKV
      EHRZODUPMKZSVLVHNZEAVEVDVLLKVNEPVMVBVCVDULVLDUMLEVLAUNUOVBVCVDVMUQTVBVCVL
      AKZIRZAKZSVLVPVGMAKZVDVBVOVQVRAVGCVLVPFVKURUSUTVAT $.
      $( [11-Dec-2014] $)

    $( Closure of the group multiple (exponentiation) operation.  TODO:  This
       can be generalized to a monoid if/when we introduce them. $)
    mulgnn0cl $p |- ( ( G e. Grp /\ N e. NN0 /\ X e. B ) -> ( N T X ) e. B ) $=
      ( cgrp wcel cn0 w3a cn cc0 wceq wo co simp2 elnn0 sylib wi 3com23 3adant2
      mulgnncl 3expia c0g cfv eqid mulg0 3ad2ant3 grpidcl 3ad2ant1 oveq1 eleq1d
      eqeltrd syl5ibrcom jaod mpd ) CHIZDJIZEAIZKZDLIZDMNZOZDEBPZAIZVAUSVDURUSU
      TQDRSVAVBVFVCURUTVBVFTUSURUTVBVFURVBUTVFABCDEFGUCUAUDUBVAVFVCMEBPZAIVAVGC
      UEUFZAUTURVGVHNUSABCVHEFVHUGZGUHUIURUSVHAIUTACVHFVIUJUKUNVCVEVGADMEBULUMU
      OUPUQ $.
      $( [11-Dec-2014] $)

    $( Closure of the group multiple (exponentiation) operation. $)
    mulgcl $p |- ( ( G e. Grp /\ N e. ZZ /\ X e. B ) -> ( N T X ) e. B ) $=
      ( cgrp wcel cz w3a cneg wa co simpl1 simpl3 syl3anc cfv wceq syl2anc eqid
      cn0 cr cn wo simp2 elznn0nn sylib simpr mulgnn0cl cminusg cc simprl recnd
      negneg syl oveq1d simprr mulgnegnn eqtr3d mulgnncl grpinvcl eqeltrd mpdan
      jaodan ) CHIZDJIZEAIZKZDUBIZDUCIZDLZUDIZMZUEZDEBNZAIZVIVGVOVFVGVHUFDUGUHV
      IVJVQVNVIVJMVFVJVHVQVFVGVHVJOVIVJUIVFVGVHVJPABCDEFGUJQVIVNMZVPVLEBNZCUKRZ
      RZAVRVLLZEBNZVPWAVRWBDEBVRDULIWBDSVRDVIVKVMUMUNDUOUPUQVRVMVHWCWASVIVKVMUR
      ZVFVGVHVNPZABCVTVLEFGVTUAZUSTUTVRVFVSAIZWAAIVFVGVHVNOZVRVFVMVHWGWHWDWEABC
      VLEFGVAQACVTVSFWFVBTVCVEVD $.
      $( [11-Dec-2014] $)

    mulgneg.i $e |- I = ( invg ` G ) $.
    $( Group multiple (exponentiation) operation at a negative integer. $)
    mulgneg $p |- ( ( G e. Grp /\ N e. ZZ /\ X e. B ) ->
      ( -u N T X ) = ( I ` ( N T X ) ) ) $=
      ( wcel cneg wa co cfv wceq cc0 simpl3 syl2anc syl oveq1d cz w3a cn0 cr cn
      cgrp simp2 elznn0nn sylib elnn0 simpr mulgnegnn c0g simpl1 grpinvid mulg0
      wo eqid eqtrd fveq2d negeqd neg0 syl6eq 3eqtr4rd jaodan simprr nnz mulgcl
      sylan2b syl3anc grpinvinv cc simprl recnd negneg eqtr3d mpdan ) CUFJZEUAJ
      ZFAJZUBZEUCJZEUDJZEKZUEJZLZUQZWDFBMZEFBMZDNZOZWAVSWGVRVSVTUGEUHUIWAWBWKWF
      WBWAEUEJZEPOZUQWKEUJWAWLWKWMWAWLLWLVTWKWAWLUKVRVSVTWLQABCDEFGHIULRWAWMLZC
      UMNZDNZWOWJWHWNVRWPWOOVRVSVTWMUNCDWOWOURZIUOSWNWIWODWNWIPFBMZWOWNEPFBWAWM
      UKZTWNVTWRWOOVRVSVTWMQABCWOFGWQHUPSZUSUTWNWHWRWOWNWDPFBWNWDPKPWNEPWSVAVBV
      CTWTUSVDVEVIWAWFLZWHDNZDNZWHWJXAVRWHAJZXCWHOVRVSVTWFUNZXAVRWDUAJZVTXDXEXA
      WEXFWAWCWEVFZWDVGSVRVSVTWFQZABCWDFGHVHVJACDWHGIVKRXAXBWIDXAWDKZFBMZXBWIXA
      WEVTXJXBOXGXHABCDWDFGHIULRXAXIEFBXAEVLJXIEOXAEWAWCWEVMVNEVOSTVPUTVPVEVQ
      $.
      $( [11-Dec-2014] $)

    $( Group multiple (exponentiation) operation at negative one. $)
    mulgm1 $p |- ( ( G e. Grp /\ X e. B ) -> ( -u 1 T X ) = ( I ` X ) ) $=
      ( cgrp wcel wa c1 cneg co cfv cz wceq 1z mulgneg mp3an2 adantl fveq2d
      mulg1 eqtrd ) CIJZEAJZKZLMEBNZLEBNZDOZEDOUELPJUFUHUJQRABCDLEFGHSTUGUIEDUF
      UIEQUEABCEFGUCUAUBUD $.
      $( [20-Dec-2014] $)
  $}

  ${
    $d x G $.  $d x N $.  $d x O $.
    mulgnn0z.b $e |- B = ( Base ` G ) $.
    mulgnn0z.t $e |- T = ( .g ` G ) $.
    mulgnn0z.o $e |- O = ( 0g ` G ) $.
    $( A group multiple of the identity, for nonnegative multiple.  TODO:  This
       can be generalized to a monoid if/when we introduce them. $)
    mulgnn0z $p |- ( ( G e. Grp /\ N e. NN0 ) -> ( N T O ) = O ) $=
      ( vx cn0 wcel cgrp cn cc0 wceq co cfv c1 eqid adantr elnn0 cplusg csn cxp
      wo wa cseq id grpidcl mulgnn syl2anr grplid mpdan cuz simpr nnuz syl6eleq
      cfz elfznn fvconst2g sylan sylan2 seqid3 eqtrd oveq1 syl sylan9eqr jaodan
      cv mulg0 sylan2b ) DJKCLKZDMKZDNOZUEDEBPZEOZDUAVLVMVPVNVLVMUFZVODCUBQZMEU
      CUDZRUGZQZEVMVMEAKZVOWAOVLVMUHACEFHUIZAVRVTBCDEFVRSZGVTSUJUKVQIVRVSRDEVLE
      EVRPEOZVMVLWBWEWCAVRCEEFWDHULUMTVQDMRUNQVLVMUOUPUQIVIZRDURPKVQWFMKZWFVSQE
      OZWFDUSVQWBWGWHVLWBVMWCTMEWFAUTVAVBVCVDVNVLVONEBPZEDNEBVEVLWBWIEOWCABCEEF
      HGVJVFVGVHVK $.
      $( [13-Dec-2014] $)

    $( A group multiple of the identity, for integer multiple. $)
    mulgz $p |- ( ( G e. Grp /\ N e. ZZ ) -> ( N T O ) = O ) $=
      ( wcel cz wa cn0 cneg co wceq adantl mulgnn0z adantlr cfv adantr wo cr cc
      cgrp elznn0 simprbi cminusg simpll nn0z grpidcl eqid mulgneg zcn ad2antlr
      syl3anc negneg oveq1d fveq2d grpinvid ad2antrr eqtrd 3eqtr3d jaodan mpdan
      syl ) CUDIZDJIZKZDLIZDMZLIZUAZDEBNZEOZVGVLVFVGDUBIVLDUEUFPVHVIVNVKVFVIVNV
      GABCDEFGHQRVHVKKZVJMZEBNZVJEBNZCUGSZSZVMEVOVFVJJIZEAIZVQVTOVFVGVKUHVKWAVH
      VJUIPVHWBVKVFWBVGACEFHUJTTABCVSVJEFGVSUKZULUOVOVPDEBVODUCIZVPDOVGWDVFVKDU
      MUNDUPVEUQVOVTEVSSZEVOVREVSVFVKVREOVGABCVJEFGHQRURVFWEEOVGVKCVSEHWCUSUTVA
      VBVCVD $.
      $( [13-Dec-2014] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d x y z M $.  $d x y z N $.  $d x y z P $.
    $d x y z X $.
    mulgnndir.b $e |- B = ( Base ` G ) $.
    mulgnndir.t $e |- T = ( .g ` G ) $.
    mulgnndir.p $e |- P = ( +g ` G ) $.
    $( Sum of group multiples, for positive multiples.  TODO:  This can be
       generalized to a semigroup if/when we introduce them. $)
    mulgnndir $p |- ( ( G e. Grp /\ ( M e. NN /\ N e. NN /\ X e. B ) ) ->
      ( ( M + N ) T X ) = ( ( M T X ) P ( N T X ) ) ) $=
      ( wcel cn wa caddc co c1 cfv wceq cuz syl2anc vx vy cgrp w3a csn cxp cseq
      vz grpcl 3expb adantlr grpass simpr2 nnuz syl6eleq simpr1 nnz syl eluzadd
      cv cz cc addcom ax-1cn sylancl fveq2d eleq12d mpbird cfz simpr3 fvconst2g
      nncn elfznn syl2an adantr eqeltrd seqsplit nnaddcl mulgnn eqtr4d seqshft2
      eqid syl2anr seqeq1d fveq12d 3eqtr4d oveq12d ) DUCKZELKZFLKZGAKZUDZMZEFNO
      ZBLGUEUFZPUGZQZEWPQZWNBWOEPNOZUGZQZBOWNGCOZEGCOZFGCOZBOWMUAUBUHBAWOPEWNWH
      UAUTZAKZUBUTZAKZMXEXGBOZAKZWLWHXFXHXJABDXEXGHJUIUJUKWHXFXHUHUTZAKUDXIXKBO
      XEXGXKBOBORWLABDXEXGXKHJULUKWMWNWSSQZKFENOZPENOZSQZKZWMFPSQZKEVAKZXPWMFLX
      QWHWIWJWKUMZUNUOZWMWIXRWHWIWJWKUPZEUQURZEPFUSTWMWNXMXLXOWMEVBKZFVBKZWNXMR
      WMWIYCYAEVLURZWMWJYDXSFVLUREFVCTZWMWSXNSWMYCPVBKWSXNRYEVDEPVCVEZVFVGVHWME
      LXQYAUNUOWMXEPWNVIOKZMXEWOQZGAWMWKXELKZYIGRZYHWHWIWJWKVJZXEWNVMLGXEAVKZVN
      WMWKYHYLVOVPVQWMWNLKZWKXBWQRWMWIWJYNYAXSEFVRTYLABWPCDWNGHJIWPWBZVSTWMXCWR
      XDXABWMWIWKXCWRRYAYLABWPCDEGHJIYOVSTWMFWPQZXMBWOXNUGZQXDXAWMBUAWOWOEPFXTY
      BWMXEPFVIOKZMZYIGXEENOZWOQZWMWKYJYKYRYLXEFVMZYMVNYSWKYTLKZUUAGRWMWKYRYLVO
      YRYJWIUUCWMUUBYAXEEVRWCLGYTAVKTVTWAWMWJWKXDYPRXSYLABWPCDFGHJIYOVSTWMWNXMW
      TYQWMWSXNBWOYGWDYFWEWFWGWF $.
      $( [11-Dec-2014] $)

    $( Sum of group multiples, generalized to ` NN0 ` .  TODO:  This can be
       generalized to a monoid if/when we introduce them. $)
    mulgnn0dir $p |- ( ( G e. Grp /\ ( M e. NN0 /\ N e. NN0 /\ X e. B ) ) ->
      ( ( M + N ) T X ) = ( ( M T X ) P ( N T X ) ) ) $=
      ( wcel wa cc0 wceq caddc co simpr syl oveq1d eqtrd cgrp cn0 w3a cn simpr1
      wo elnn0 sylib simpr2 adantr simplll simplr simpr3 ad2antrr mulgnndir c0g
      syl13anc cfv simpll nn0z simplr3 mulgcl syl3anc eqid grprid syl2anc mulg0
      cz oveq2d nn0cn addid1 3syl 3eqtr4rd adantlr jaodan simplr2 grplid addid2
      cc mpdan ) DUAKZEUBKZFUBKZGAKZUCZLZEUDKZEMNZUFZEFOPZGCPZEGCPZFGCPZBPZNZWF
      WBWIWAWBWCWDUEZEUGUHWFWGWOWHWFWGLZFUDKZFMNZUFZWOWFWTWGWFWCWTWAWBWCWDUIFUG
      UHUJWQWRWOWSWQWRLWAWGWRWDWOWAWEWGWRUKWFWGWRULWQWRQWFWDWGWRWAWBWCWDUMUNABC
      DEFGHIJUOUQWFWSWOWGWFWSLZWLDUPURZBPZWLWNWKXAWAWLAKZXCWLNWAWEWSUSZXAWAEVHK
      ZWDXDXEXAWBXFWFWBWSWPUJZEUTRWBWCWDWAWSVAZACDEGHIVBVCABDXBWLHJXBVDZVEVFXAW
      MXBWLBXAWMMGCPZXBXAFMGCWFWSQZSXAWDXJXBNZXHACDXBGHXIIVGZRTVIXAWJEGCXAWJEMO
      PZEXAFMEOXKVIXAWBEVSKXNENXGEVJEVKVLTSVMVNVOVTWFWHLZXBWMBPZWMWNWKXOWAWMAKZ
      XPWMNWAWEWHUSZXOWAFVHKZWDXQXRXOWCXSWBWCWDWAWHVPZFUTRWBWCWDWAWHVAZACDFGHIV
      BVCABDXBWMHJXIVQVFXOWLXBWMBXOWLXJXBXOEMGCWFWHQZSXOWDXLYAXMRTSXOWJFGCXOWJM
      FOPZFXOEMFOYBSXOWCFVSKYCFNXTFVJFVRVLTSVMVOVT $.
      $( [11-Dec-2014] $)

    $( Lemma for ~ mulgdir . $)
    mulgdirlem $p |- ( ( G e. Grp /\ ( M e. ZZ /\ N e. ZZ /\ X e. B ) /\
      ( M + N ) e. NN0 ) -> ( ( M + N ) T X ) = ( ( M T X ) P ( N T X ) ) ) $=
      ( wcel cz co cn0 wceq syl wa adantr syl3anc syl2anc cgrp w3a caddc simp21
      cneg wo cr elznn0 simprbi simp22 simpl1 simprl simprr mulgnn0dir syl13anc
      simpl23 anassrs simpl3 nn0z mulgcl ad2antll grpass zcn addcl negsub pncan
      cmin cc eqtrd oveq1d eqtr3d c0g cfv cminusg mulgneg grplinv oveq2d grprid
      eqid 3eqtr3rd jaodan mpdan znegcl simp3 grprinv grplid simpr negcl addcom
      wi pncan2 3eqtrd 3eqtr3d idi ) DUAKZELKZFLKZGAKZUBZEFUCMZNKZUBZENKZEUEZNK
      ZUFZWTGCMZEGCMZFGCMZBMZOZXBWPXFWOWPWQWRXAUDZWPEUGKXFEUHUIPXBXCXKXEXBXCQZF
      NKZFUEZNKZUFZXKXBXQXCXBWQXQWOWPWQWRXAUJZWQFUGKXQFUHUIPRXMXNXKXPXBXCXNXKXB
      XCXNQZQWOXCXNWRXKWOWSXAXSUKXBXCXNULXBXCXNUMWPWQWRWOXAXSUPABCDEFGHIJUNUOUQ
      XBXCXPXKXBXCXPQZQZXGXOGCMZBMZXIBMZXGYBXIBMZBMZXJXGYAWOXGAKZYBAKZXIAKZYDYF
      OWOWSXAXTUKZYAWOWTLKZWRYGYJYAXAYKWOWSXAXTURZWTUSZPWPWQWRWOXAXTUPZACDWTGHI
      UTZSZYAWOXOLKZWRYHYJXPYQXBXCXOUSVAYNACDXOGHIUTSYAWOWQWRYIYJXBWQXTXRRZYNAC
      DFGHIUTSZABDXGYBXIHJVBUOYAYCXHXIBYAWTXOUCMZGCMZYCXHYAWOXAXPWRUUAYCOYJYLXB
      XCXPUMYNABCDWTXOGHIJUNUOYAYTEGCYAYTWTFVGMZEYAWTVHKZFVHKZYTUUBOXBUUCXTXBEV
      HKZUUDUUCXBWPUUEXLEVCPZXBWQUUDXRFVCPZEFVDTZRXBUUDXTUUGRZWTFVETYAUUEUUDUUB
      EOXBUUEXTUUFRUUIEFVFTVIVJVKVJYAYFXGDVLVMZBMZXGYAYEUUJXGBYAYEXIDVNVMZVMZXI
      BMZUUJYAYBUUMXIBYAWOWQWRYBUUMOYJYRYNACDUULFGHIUULVSZVOSVJYAWOYIUUNUUJOYJY
      SABDUULUUJXIHJUUJVSZUUOVPTVIVQYAWOYGUUKXGOYJYPABDUUJXGHJUUPVRTVIVTUQWAWBX
      BXEQZXKWJUUQXHXDGCMZBMZXGBMZXHUURXGBMZBMZXGXJUUQWOXHAKZUURAKZYGUUTUVBOWOW
      SXAXEUKZUUQWOWPWRUVCUVEXBWPXEXLRZWPWQWRWOXAXEUPZACDEGHIUTSZUUQWOXDLKZWRUV
      DUVEUUQWPUVIUVFEWCPUVGACDXDGHIUTSUUQWOYKWRYGUVEXBYKXEXBXAYKWOWSXAWDYMPRUV
      GYOSZABDXHUURXGHJVBUOUUQUUTUUJXGBMZXGUUQUUSUUJXGBUUQUUSXHXHUULVMZBMZUUJUU
      QUURUVLXHBUUQWOWPWRUURUVLOUVEUVFUVGACDUULEGHIUUOVOSVQUUQWOUVCUVMUUJOUVEUV
      HABDUULUUJXHHJUUPUUOWETVIVJUUQWOYGUVKXGOUVEUVJABDUUJXGHJUUPWFTVIUUQUVAXIX
      HBUUQXDWTUCMZGCMZUVAXIUUQWOXEXAWRUVOUVAOUVEXBXEWGWOWSXAXEURUVGABCDXDWTGHI
      JUNUOUUQUVNFGCUUQUVNWTXDUCMZWTEVGMZFUUQXDVHKZUUCUVNUVPOUUQUUEUVRXBUUEXEUU
      FRZEWHPXBUUCXEUUHRZXDWTWITUUQUUCUUEUVPUVQOUVTUVSWTEVETUUQUUEUUDUVQFOUVSXB
      UUDXEUUGREFWKTWLVJVKVQWMWNWAWB $.
      $( [13-Dec-2014] $)

    $( Sum of group multiples, generalized to ` ZZ ` . $)
    mulgdir $p |- ( ( G e. Grp /\ ( M e. ZZ /\ N e. ZZ /\ X e. B ) ) ->
      ( ( M + N ) T X ) = ( ( M T X ) P ( N T X ) ) ) $=
      ( wcel cz co cn0 wceq syl2anc syl cfv cc syl3anc cgrp w3a wa caddc simpr1
      wo simpr2 zaddcl cr elznn0 simprbi mulgdirlem 3expa cminusg simpll adantr
      cneg znegcl simplr3 zcn negdi negcl addcom eqtrd simpr eqeltrrd syl131anc
      oveq1d eqid mulgneg eqtr3d oveq12d mulgcl grpinvadd eqtr4d 3eqtr3d fveq2d
      grpinvinv grpcl jaodan mpdan ) DUAKZELKZFLKZGAKZUBZUCZEFUDMZNKZWHUQZNKZUF
      ZWHGCMZEGCMZFGCMZBMZOZWGWHLKZWLWGWCWDWRWBWCWDWEUEZWBWCWDWEUGZEFUHPZWRWHUI
      KWLWHUJUKQWGWIWQWKWBWFWIWQABCDEFGHIJULUMWGWKUCZWMDUNRZRZXCRZWPXCRZXCRZWMW
      PXBXDXFXCXBFUQZEUQZUDMZGCMZXHGCMZXIGCMZBMZXDXFXBWBXHLKZXILKZWEXJNKXKXNOWB
      WFWKUOZXBWDXOWGWDWKWTUPZFURQXBWCXPWGWCWKWSUPZEURQWCWDWEWBWKUSZXBWJXJNXBWJ
      XIXHUDMZXJXBESKZFSKZWJYAOXBWCYBXSEUTQZXBWDYCXRFUTQZEFVAPXBXISKZXHSKZYAXJO
      XBYBYFYDEVBQXBYCYGYEFVBQXIXHVCPVDZWGWKVEVFABCDXHXIGHIJULVGXBWJGCMZXKXDXBW
      JXJGCYHVHXBWBWRWEYIXDOXQWGWRWKXAUPZXTACDXCWHGHIXCVIZVJTVKXBXNWOXCRZWNXCRZ
      BMZXFXBXLYLXMYMBXBWBWDWEXLYLOXQXRXTACDXCFGHIYKVJTXBWBWCWEXMYMOXQXSXTACDXC
      EGHIYKVJTVLXBWBWNAKZWOAKZXFYNOXQXBWBWCWEYOXQXSXTACDEGHIVMTZXBWBWDWEYPXQXR
      XTACDFGHIVMTZABDXCWNWOHJYKVNTVOVPVQXBWBWMAKZXEWMOXQXBWBWRWEYSXQYJXTACDWHG
      HIVMTADXCWMHYKVRPXBWBWPAKZXGWPOXQXBWBYOYPYTXQYQYRABDWNWOHJVSTADXCWPHYKVRP
      VPVTWA $.
      $( [13-Dec-2014] $)

    $( Group multiple (exponentiation) operation at a successor, extended to
       ` ZZ ` . $)
    mulgp1 $p |- ( ( G e. Grp /\ N e. ZZ /\ X e. B ) ->
      ( ( N + 1 ) T X ) = ( ( N T X ) P X ) ) $=
      ( cgrp wcel cz w3a c1 caddc co wceq 1z mulgdir mp3anr2 3impb mulg1 oveq2d
      3ad2ant3 eqtrd ) DJKZELKZFAKZMZENOPFCPZEFCPZNFCPZBPZUKFBPUFUGUHUJUMQZUFUG
      NLKUHUNRABCDENFGHISTUAUIULFUKBUHUFULFQUGACDFGHUBUDUCUE $.
      $( [11-Dec-2014] $)
  $}

  ${
    $d n x B $.  $d n x G $.  $d n x I $.  $d x N $.  $d n x T $.  $d n x X $.
    mulgneg2.b $e |- B = ( Base ` G ) $.
    mulgneg2.m $e |- T = ( .g ` G ) $.
    mulgneg2.i $e |- I = ( invg ` G ) $.
    $( Group multiple (exponentiation) operation at a negative integer. $)
    mulgneg2 $p |- ( ( G e. Grp /\ N e. ZZ /\ X e. B ) ->
      ( -u N T X ) = ( N T ( I ` X ) ) ) $=
      ( wcel cneg co cfv wceq cc0 c1 negeq oveq1d oveq1 eqeq12d vx vn cgrp neg0
      cz cv caddc wa syl6eq c0g eqid mulg0 adantl grpinvcl syl eqtr4d wi cplusg
      cn0 cc nn0cn ax-1cn negdi sylancl simpll nn0negz 1z znegcl simplr mulgdir
      a1i syl13anc mulgm1 adantr oveq2d 3eqtrd simpr mulgnn0p1 syl3anc ex fveq2
      syl5ibr cn nnnegz mulgneg id mulgnegnn syl2anr zindd 3impia 3com23 ) CUCJ
      ZFAJZEUEJZEKZFBLZEFDMZBLZNZWLWMWNWSUAUFZKZFBLZWTWQBLZNOFBLZOWQBLZNUBUFZKZ
      FBLZXFWQBLZNZXGKZFBLZXGWQBLZNZXFPUGLZKZFBLZXOWQBLZNZWSWLWMUHZUAUBEWTONZXB
      XDXCXEYAXAOFBYAXAOKOWTOQUDUIRWTOWQBSTWTXFNZXBXHXCXIYBXAXGFBWTXFQRWTXFWQBS
      TWTXONZXBXQXCXRYCXAXPFBWTXOQRWTXOWQBSTWTXGNZXBXLXCXMYDXAXKFBWTXGQRWTXGWQB
      STWTENZXBWPXCWRYEXAWOFBWTEQRWTEWQBSTXTXDCUJMZXEWMXDYFNWLABCYFFGYFUKZHULUM
      XTWQAJZXEYFNACDFGIUNZABCYFWQGYGHULUOUPXTXFUSJZXJXSUQXJXSXTYJUHZXHWQCURMZL
      ZXIWQYLLZNXHXIWQYLSYKXQYMXRYNYKXQXGPKZUGLZFBLZXHYOFBLZYLLZYMYKXPYPFBYKXFU
      TJZPUTJXPYPNYJYTXTXFVAUMVBXFPVCVDRYKWLXGUEJZYOUEJZWMYQYSNWLWMYJVEZYJUUAXT
      XFVFUMYKPUEJZUUBUUDYKVGVKPVHUOWLWMYJVIAYLBCXGYOFGHYLUKZVJVLYKYRWQXHYLXTYR
      WQNYJABCDFGHIVMVNVOVPYKWLYJYHXRYNNUUCXTYJVQXTYHYJYIVNAYLBCXFWQGHUUEVRVSTW
      BVTXTXFWCJZXJXNUQXJXNXTUUFUHZXHDMZXIDMZNXHXIDWAUUGXLUUHXMUUIUUGWLUUAWMXLU
      UHNWLWMUUFVEUUFUUAXTXFWDUMWLWMUUFVIABCDXGFGHIWEVSUUFUUFYHXMUUINXTUUFWFYIA
      BCDXFWQGHIWGWHTWBVTWIWJWK $.
      $( [13-Dec-2014] $)
  $}

  ${
    $d m n B $.  $d m n G $.  $d n M $.  $d m n N $.  $d m n T $.  $d m n X $.
    mulgass.b $e |- B = ( Base ` G ) $.
    mulgass.t $e |- T = ( .g ` G ) $.
    $( Product of group multiples, for positive multiples.  TODO:  This can be
       generalized to a semigroup if/when we introduce them. $)
    mulgnnass $p |- ( ( G e. Grp /\ ( M e. NN /\ N e. NN /\ X e. B ) ) ->
      ( ( M x. N ) T X ) = ( M T ( N T X ) ) ) $=
      ( wcel cn cmul co wceq wi c1 caddc oveq1 oveq1d eqeq12d imbi2d vn vm cgrp
      w3a cv cc nncn mulid2 syl 3ad2ant1 mulgnncl 3coml mulg1 eqtr4d cplusg cfv
      wa adantr ax-1cn simpr1 adddir syl3anc adantl oveq2d eqtrd simpr3 nnmulcl
      3ad2antr1 simpr2 eqid mulgnndir syl13anc mulgnnp1 sylan2 syl5ibr ex nnind
      a1i a2d 3expd com4r 3imp2 ) CUCIZDJIZEJIZFAIZDEKLZFBLZDEFBLZBLZMZWDWEWFWC
      WKWDWEWFWCWKWEWFWCUDZUAUEZEKLZFBLZWMWIBLZMZNWLOEKLZFBLZOWIBLZMZNWLUBUEZEK
      LZFBLZXBWIBLZMZNWLXBOPLZEKLZFBLZXGWIBLZMZNWLWKNUAUBDWMOMZWQXAWLXLWOWSWPWT
      XLWNWRFBWMOEKQRWMOWIBQSTWMXBMZWQXFWLXMWOXDWPXEXMWNXCFBWMXBEKQRWMXBWIBQSTW
      MXGMZWQXKWLXNWOXIWPXJXNWNXHFBWMXGEKQRWMXGWIBQSTWMDMZWQWKWLXOWOWHWPWJXOWNW
      GFBWMDEKQRWMDWIBQSTWLWSWIWTWLWREFBWEWFWREMZWCWEEUFIZXPEUGZEUHUIUJZRWLWIAI
      ZWTWIMWCWEWFXTABCEFGHUKULZABCWIGHUMUIUNXBJIZWLXFXKYBWLXFXKNXFXKYBWLUQZXDW
      ICUOUPZLZXEWIYDLZMXDXEWIYDQYCXIYEXJYFYCXIXCEPLZFBLZYEYCXHYGFBYCXHXCWRPLZY
      GYCXBUFIZOUFIZXQXHYIMYBYJWLXBUGURYKYCUSVRYCWEXQYBWEWFWCUTZXRUIXBOEVAVBYCW
      REXCPWLXPYBXSVCVDVERYCWCXCJIZWEWFYHYEMYBWEWFWCVFYBWFWEYMWCXBEVGVHYLYBWEWF
      WCVIAYDBCXCEFGHYDVJZVKVLVEWLYBXTXJYFMYAAYDBCXBWIGHYNVMVNSVOVPVSVQVTWAWB
      $.
      $( [13-Dec-2014] $)

    $( Product of group multiples, generalized to ` NN0 ` .  TODO:  This can be
       generalized to a monoid if/when we introduce them. $)
    mulgnn0ass $p |- ( ( G e. Grp /\ ( M e. NN0 /\ N e. NN0 /\ X e. B ) ) ->
      ( ( M x. N ) T X ) = ( M T ( N T X ) ) ) $=
      ( wcel cn0 wa cn cc0 wceq wo cmul co adantr oveq1d oveq1 w3a simpr1 elnn0
      cgrp sylib simpr2 simpl simprl simprr simpr3 mulgnnass syl13anc expr eqid
      c0g cfv mulg0 syl cc nn0cn mul01 3syl oveq2d mulgnn0z syl2anc eqtrd oveq2
      3eqtr4d eqeq12d syl5ibrcom jaod mpd ex mul02 mulgnn0cl syl3anc ) CUDIZDJI
      ZEJIZFAIZUAZKZDLIZDMNZOZDEPQZFBQZDEFBQZBQZNZWBVRWEVQVRVSVTUBZDUCUEWBWCWJW
      DWBWCWJWBWCKZELIZEMNZOZWJWBWOWCWBVSWOVQVRVSVTUFZEUCUERWLWMWJWNWBWCWMWJWBW
      CWMKZKVQWCWMVTWJWBVQWQVQWAUGZRWBWCWMUHWBWCWMUIWBVTWQVQVRVSVTUJZRABCDEFGHU
      KULUMWLWJWNDMPQZFBQZDMFBQZBQZNZWBXDWCWBXBCUOUPZXAXCWBVTXBXENWSABCXEFGXEUN
      ZHUQURZWBWTMFBWBVRDUSIWTMNWKDUTDVAVBSWBXCDXEBQZXEWBXBXEDBXGVCWBVQVRXHXENW
      RWKABCDXEGHXFVDVEVFVHRWNWGXAWIXCWNWFWTFBEMDPVGSWNWHXBDBEMFBTVCVIVJVKVLVMW
      BWJWDMEPQZFBQZMWHBQZNWBXBXEXJXKXGWBXIMFBWBVSEUSIXIMNWPEUTEVNVBSWBWHAIZXKX
      ENWBVQVSVTXLWRWPWSABCEFGHVOVPABCXEWHGXFHUQURVHWDWGXJWIXKWDWFXIFBDMEPTSDMW
      HBTVIVJVKVL $.
      $( [13-Dec-2014] $)

    $( Product of group multiples, generalized to ` ZZ ` . $)
    mulgass $p |- ( ( G e. Grp /\ ( M e. ZZ /\ N e. ZZ /\ X e. B ) ) ->
      ( ( M x. N ) T X ) = ( M T ( N T X ) ) ) $=
      ( wcel cz wa cn0 cneg cmul co wceq syl2anc adantr cfv syl3anc cgrp w3a wo
      simpr1 cr elznn0 simprbi syl simpr2 simpll simprl simprr simplr3 syl13anc
      mulgnn0ass ex zcn mulneg1 oveq1d simpl simpr3 eqtr3d cminusg fveq2 zmulcl
      cc eqid mulgneg fveq2d mulgcl grpinvinv eqeq12d syl5ib imp syldan mulneg2
      oveq2d mulgneg2 eqtr4d 3eqtr3d mul2neg nn0z ad2antll negneg ccased mp2and
      eqtrd 3syl ) CUAIZDJIZEJIZFAIZUBZKZDLIZDMZLIZUCZELIZEMZLIZUCZDENOZFBOZDEF
      BOZBOZPZWNWJWRWIWJWKWLUDZWJDUEIWRDUFUGUHWNWKXBWIWJWKWLUIZWKEUEIXBEUFUGUHW
      NWOWSWQXAXGWNWOWSKZXGWNXJKWIWOWSWLXGWIWMXJUJWNWOWSUKWNWOWSULWJWKWLWIXJUMA
      BCDEFGHUOUNUPWNWQWSKZXGWNXKXCMZFBOZWPXEBOZPZXGWNXKKZWPENOZFBOZXMXNXPXQXLF
      BWNXQXLPZXKWNDVFIZEVFIZXSWNWJXTXHDUQUHZWNWKYAXIEUQZUHZDEURQRUSXPWIWQWSWLX
      RXNPWNWIXKWIWMUTZRWNWQWSUKWNWQWSULWNWLXKWIWJWKWLVAZRABCWPEFGHUOUNVBWNXOXG
      XOXMCVCSZSZXNYGSZPWNXGXMXNYGVDWNYHXDYIXFWNYHXDYGSZYGSZXDWNXMYJYGWNWIXCJIZ
      WLXMYJPYEWNWJWKYLXHXIDEVEQZYFABCYGXCFGHYGVGZVHTVIWNWIXDAIZYKXDPYEWNWIYLWL
      YOYEYMYFABCXCFGHVJTACYGXDGYNVKQWGWNYIXFYGSZYGSZXFWNXNYPYGWNWIWJXEAIZXNYPP
      YEXHWNWIWKWLYRYEXIYFABCEFGHVJTZABCYGDXEGHYNVHTVIWNWIXFAIZYQXFPYEWNWIWJYRY
      TYEXHYSABCDXEGHVJTACYGXFGYNVKQWGVLVMVNZVOUPWNWOXAKZXGWNUUBXOXGWNUUBKZDWTN
      OZFBOZDWTFBOZBOZXMXNUUCWIWOXAWLUUEUUGPWNWIUUBYERWNWOXAUKWNWOXAULWNWLUUBYF
      RABCDWTFGHUOUNUUCUUDXLFBWNUUDXLPZUUBWNXTYAUUHYBYDDEVPQRUSWNUUGXNPUUBWNUUG
      DXEYGSZBOZXNWNUUFUUIDBWNWIWKWLUUFUUIPYEXIYFABCYGEFGHYNVHTVQWNWIWJYRXNUUJP
      YEXHYSABCYGDXEGHYNVRTVSRVTUUAVOUPWNWQXAKZXGWNUUKKZWPWTNOZFBOZWPUUFBOZXDXF
      UULWIWQXAWLUUNUUOPWNWIUUKYERZWNWQXAUKWNWQXAULWNWLUUKYFRZABCWPWTFGHUOUNWNU
      UNXDPUUKWNUUMXCFBWNXTYAUUMXCPYBYDDEWAQUSRUULUUODUUFYGSZBOZXFUULWIWJUUFAIZ
      UUOUUSPUUPWNWJUUKXHRUULWIWTJIZWLUUTUUPXAUVAWNWQWTWBWCZUUQABCWTFGHVJTABCYG
      DUUFGHYNVRTUULUURXEDBUULWTMZFBOZUURXEUULWIUVAWLUVDUURPUUPUVBUUQABCYGWTFGH
      YNVHTUULUVCEFBWNUVCEPZUUKWNWKYAUVEXIYCEWDWHRUSVBVQWGVTUPWEWF $.
      $( [13-Dec-2014] $)
  $}

  ${
    mulgsubdir.b $e |- B = ( Base ` G ) $.
    mulgsubdir.t $e |- T = ( .g ` G ) $.
    mulgsubdir.d $e |- D = ( -g ` G ) $.
    $( Subtraction of a group element from itself. $)
    mulgsubdir $p |- ( ( G e. Grp /\ ( M e. ZZ /\ N e. ZZ /\ X e. B ) ) ->
      ( ( M - N ) T X ) = ( ( M T X ) D ( N T X ) ) ) $=
      ( wcel cz co cfv wceq syl eqid cc zcn syl2anc cgrp w3a wa cneg caddc cmin
      cplusg simpr1 simpr2 znegcl simpr3 mulgdir syl13anc negsub oveq1d cminusg
      simpl mulgneg 3adant3r1 oveq2d mulgcl 3adant3r2 grpsubval eqtr4d 3eqtr3d
      ) DUAKZELKZFLKZGAKZUBZUCZEFUDZUEMZGCMZEGCMZVLGCMZDUGNZMZEFUFMZGCMVOFGCMZB
      MZVKVFVGVLLKZVIVNVROVFVJUQVFVGVHVIUHZVKVHWBVFVGVHVIUIZFUJPVFVGVHVIUKAVQCD
      EVLGHIVQQZULUMVKVMVSGCVKERKZFRKZVMVSOVKVGWFWCESPVKVHWGWDFSPEFUNTUOVKVRVOV
      TDUPNZNZVQMZWAVKVPWIVOVQVFVHVIVPWIOVGACDWHFGHIWHQZURUSUTVKVOAKZVTAKZWAWJO
      VFVGVIWLVHACDEGHIVAVBVFVHVIWMVGACDFGHIVAUSAVQDWHBVOVTHWEWKJVCTVDVE $.
      $( [13-Dec-2014] $)
  $}

