$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a g m n s w x y $.
    $( Define class of all groups. $)
    df-grp $a |- Grp = { g e. Mnd | A. a e. ( Base ` g ) E. m e. ( Base ` g )
      ( m ( +g ` g ) a ) = ( 0g ` g ) } $.

    $( Define inverse of group element. $)
    df-minusg $a |- invg = ( g e. _V |-> ( x e. ( Base ` g )
  |-> ( iota_ w e. ( Base ` g ) ( w ( +g ` g ) x ) = ( 0g ` g ) ) ) ) $.

    $( Define group subtraction (also called division for multiplicative
       groups). $)
    df-sbg $a |- -g = ( g e. _V |-> ( x e. ( Base ` g ) , y e. ( Base ` g )
  |-> ( x ( +g ` g ) ( ( invg ` g ) ` y ) ) ) ) $.

    $( Define the group multiple function, also known as group exponentiation
       when viewed multiplicatively. $)
    df-mulg $a |- .g = ( g e. _V |-> ( n e. ZZ , x e. ( Base ` g ) |->
    if ( n = 0 , ( 0g ` g ) , [_ seq 1 ( ( +g ` g ) , ( NN X. { x } ) ) / s ]_
      if ( 0 < n , ( s ` n ) , ( ( invg ` g ) ` ( s ` -u n ) ) ) ) ) ) $.
  $}

  ${
    $d a g m B $.  $d a g m G $.  $d g P $.  $d g Z $.
    isgrp.b $e |- B = ( Base ` G ) $.
    isgrp.p $e |- P = ( +g ` G ) $.
    isgrp.z $e |- Z = ( 0g ` G ) $.
    $( The predicate "is a group."  (Revised by Mario Carneiro, 6-Jan-2015.) $)
    isgrp $p |- ( G e. Grp <->
      ( G e. Mnd /\ A. a e. B E. m e. B ( m P a ) = Z ) ) $=
      ( vg cv cplusg cfv co c0g wceq cbs wrex fveq2 syl6eqr wral cmnd rexeqbidv
      cgrp oveqd eqeq12d raleqbidv df-grp elrab2 ) CKZFKZJKZLMZNZULOMZPZCULQMZR
      ZFUQUAUJUKBNZEPZCARZFAUAJDUBUDULDPZURVAFUQAVBUQDQMAULDQSGTZVBUPUTCUQAVCVB
      UNUSUOEVBUMBUJUKVBUMDLMBULDLSHTUEVBUODOMEULDOSITUFUCUGJCFUHUI $.
      $( [6-Jan-2015] $) $( [17-Oct-2012] $)
  $}

  ${
    $d u x y B $.  $d a m u x y G $.  $d u x P $.  $d x y X $.  $d x Z $.
    $( A group is a monoid.  (Contributed by Mario Carneiro, 6-Jan-2015.) $)
    grpmnd $p |- ( G e. Grp -> G e. Mnd ) $=
      ( vm va cgrp wcel cmnd cv cplusg cfv co c0g wceq cbs wrex wral eqid isgrp
      simplbi ) ADEAFEBGCGAHIZJAKIZLBAMIZNCUAOUASBATCUAPSPTPQR $.
      $( [6-Jan-2015] $) $( [17-Oct-2012] $)

    grpcl.b $e |- B = ( Base ` G ) $.
    grpcl.p $e |- P = ( +g ` G ) $.
    $( Closure of the operation of a group. $)
    grpcl $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( cgrp wcel cmnd co grpmnd mndcl syl3an1 ) CHICJIDAIEAIDEBKAICLABCDEFGMN
      $.
      $( [14-Aug-2011] $)

    $( A group operation is associative. $)
    grpass $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
              ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) $=
      ( cgrp wcel cmnd w3a co wceq grpmnd mndass sylan ) CIJCKJDAJEAJFAJLDEBMFB
      MDEFBMBMNCOABCDEFGHPQ $.
      $( [14-Aug-2011] $)

    grpinvex.p $e |- Z = ( 0g ` G ) $.
    $( Every member of a group has a left inverse. $)
    grpinvex $p |- ( ( G e. Grp /\ X e. B ) -> E. y e. B ( y P X ) = Z ) $=
      ( vx cgrp wcel cv co wceq wrex wral cmnd isgrp simprbi oveq2 eqeq1d sylan
      rexbidv rcla4cva ) DKLZAMZJMZCNZFOZABPZJBQZEBLUGECNZFOZABPZUFDRLULBCADFJG
      HISTUKUOJEBUHEOZUJUNABUPUIUMFUHEUGCUAUBUDUEUC $.
      $( [6-Jan-2015] $) $( [16-Aug-2011] $)

    $( The two-sided identity element of a group is unique.  Lemma 2.2.1(a) of
       [Herstein] p. 55.  (Contributed by Mario Carneiro, 8-Dec-2014.) $)
    grpideu $p |- ( G e. Grp ->
      E! u e. B A. x e. B ( ( u P x ) = x /\ ( x P u ) = x ) ) $=
      ( cgrp wcel cmnd cv co wceq wa wral wreu grpmnd mndideu syl ) EJKELKBMZAM
      ZDNUCOUCUBDNUCOPACQBCRESABCDEGHTUA $.
      $( [8-Dec-2014] $)
  $}

  ${
    $d x y B $.  $d x y K $.  $d x y L $.  $d x y ph $.
    grppropd.1 $e |- ( ph -> B = ( Base ` K ) ) $.
    grppropd.2 $e |- ( ph -> B = ( Base ` L ) ) $.
    grppropd.3 $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
      ( x ( +g ` K ) y ) = ( x ( +g ` L ) y ) ) $.
    $( If two structures have the same group components (properties), one is a
       group iff the other one is.  (Contributed by Stefan O'Rear,
       27-Nov-2014.) $)
    grppropd $p |- ( ph -> ( K e. Grp <-> L e. Grp ) ) $=
      ( cmnd wcel cgrp wb wa cfv wceq wrex wral adantr eqid grpmnd a1i mndpropd
      wi syl5ibr cv cplusg co c0g cbs adantlr cvv elex adantl biimpa grpidpropd
      eqeq12d anassrs an32s rexbidva ralbidva rexeqdv raleqbidv 3bitr3d anbi12d
      syl isgrp 3bitr4g ex pm5.21ndd ) AEJKZELKZFLKZVLVKUDAEUAUBVMVKAFJKZFUAABC
      DEFGHIUCZUEAVKVLVMMAVKNZVKBUFZCUFZEUGOZUHZEUIOZPZBEUJOZQZCWCRZNVNVQVRFUGO
      ZUHZFUIOZPZBFUJOZQZCWJRZNVLVMVPVKVNWEWLAVKVNMVKVOSVPWBBDQZCDRWIBDQZCDRWEW
      LVPWMWNCDVPVRDKZNWBWIBDVPVQDKZWOWBWIMZVPWPWOWQVPWPWONZNVTWGWAWHAWRVTWGPVK
      IUKZVPWAWHPWRVPBCDEFADWCPVKGSZADWJPVKHSZWSVKEULKAEJUMUNVPVNFULKAVKVNVOUOF
      JUMVFUPSUQURUSUTVAVPWMWDCDWCWTVPWBBDWCWTVBVCVPWNWKCDWJXAVPWIBDWJXAVBVCVDV
      EWCVSBEWACWCTVSTWATVGWJWFBFWHCWJTWFTWHTVGVHVIVJ $.
      $( [6-Jan-2015] $) $( [27-Nov-2014] $)
  $}

  ${
    $d x y K $.  $d x y L $.
    grpprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    grpprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    $( If two structures have the same group components (properties), one is a
       group iff the other one is. $)
    grpprop $p |- ( K e. Grp <-> L e. Grp ) $=
      ( vx vy cgrp wcel wb wtru cbs cfv eqidd wceq a1i cv cplusg co wa oveqi
      grppropd trud ) AGHBGHIJEFAKLZABJUCMUCBKLNJCOEPZFPZAQLZRUDUEBQLZRNJUDUCHU
      EUCHSSUFUGUDUEDTOUAUB $.
      $( [11-Oct-2013] $)
  $}

  ${
    grpstr.b $e |- ( Base ` K ) = B $.
    grpstr.p $e |- ( +g ` K ) = P $.
    grpstr.l $e |- L
          = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( Generalize a specific 2-element group ` L ` to show that any set ` K `
       with the same (relevant) properties is also a group. $)
    grpstr $p |- ( K e. Grp <-> L e. Grp ) $=
      ( cbs cfv cvv wcel wceq fvex eqeltrri grpbase ax-mp eqtri cplusg grpplusg
      grpprop ) CDCHIZADHIZEAJKAUBLUAAJECHMNABDJGOPQCRIZBDRIZFBJKBUDLUCBJFCRMNA
      BDJGSPQT $.
      $( [6-Jan-2015] $) $( [28-Oct-2012] $)
  $}

  ${
    grpss.g $e |- G = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    grpss.r $e |- R e. _V $.
    grpss.s $e |- G C_ R $.
    grpss.f $e |- Fun R $.
    $( Show that a structure extending a constructed group (e.g. a ring) is
       also a group.  This allows us to prove that a constructed potential ring
       ` R ` is a group before we know that it is also a ring.  (Theorem
       ~ rnggrp , on the other hand, requires that we know in advance that
       ` R ` is a ring.) $)
    grpss $p |- ( G e. Grp <-> R e. Grp ) $=
      ( vs cgrp wcel cbs baseid cnx cfv cop cplusg opex eleqtrri strss plusgid
      cpr prid1 prid2 grpprop bicomi ) CJKDJKCDADCLIFHGIMNLOZAPZUHNQOZBPZUBZDUH
      UJUGARUCESTBDCQIFHGIUAUJUKDUHUJUIBRUDESTUEUF $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d y N $.  $d x y P $.  $d x y ph $.  $d y Z $.
    isgrpd2.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrpd2.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrpd2.z $e |- ( ph -> Z = ( 0g ` G ) ) $.
    isgrpd2.g $e |- ( ph -> G e. Mnd ) $.
    ${
      isgrpd2e.n $e |- ( ( ph /\ x e. B ) -> E. y e. B ( y P x ) = Z ) $.
      $( Deduce a group from its properties.  In this version of ~ isgrpd2 , we
         don't assume there is an expression for the inverse of ` x ` . $)
      isgrpd2e $p |- ( ph -> G e. Grp ) $=
        ( wcel cv cfv co wceq wrex wral eqid cmnd cplusg c0g cbs cgrp ralrimiva
        oveqd eqeq12d rexeqbidv raleqbidv mpbid isgrp sylanbrc ) AFUAMCNZBNZFUB
        OZPZFUCOZQZCFUDOZRZBUTSZFUEMKAUNUOEPZGQZCDRZBDSVBAVEBDLUFAVEVABDUTHAVDU
        SCDUTHAVCUQGURAEUPUNUOIUGJUHUIUJUKUTUPCFURBUTTUPTURTULUM $.
        $( [10-Aug-2013] $)
    $}

    isgrpd2.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpd2.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Deduce a group from its properties. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpd2 $p |- ( ph -> G e. Grp ) $=
      ( vy cv wcel wa co wceq wrex oveq1 eqeq1d rcla4ev syl2anc isgrpd2e ) ABNC
      DEGHIJKABOZCPQFCPFUFDRZGSZNOZUFDRZGSZNCTLMUKUHNFCUIFSUJUGGUIFUFDUAUBUCUDU
      E $.
      $( [10-Aug-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d x y z P $.  $d x y z ph $.  $d x y z Z $.
    $d y N $.
    isgrpd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrpd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrpd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpd.z $e |- ( ph -> Z e. B ) $.
    isgrpd.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    ${
      isgrpde.n $e |- ( ( ph /\ x e. B ) -> E. y e. B ( y P x ) = Z ) $.
      $( Deduce a group from its properties.  In this version of ~ isgrpd , we
         don't assume there is an expression for the inverse of ` x ` . $)
      isgrpde $p |- ( ph -> G e. Grp ) $=
        ( grpridd grpidd ismndd isgrpd2e ) ABCEFGHIJABEFGHIJMNABCDEFHKMNLOPZQAB
        CDEFGHIJKLMNTROS $.
        $( [6-Jan-2015] $)
    $}

    isgrpd.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpd.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Deduce a group from its properties.  Unlike ~ isgrpd2 , this one goes
       straight from the base properties rather than going through ` Mnd ` .
       ` N ` (negative) is normally dependent on ` x ` i.e. read it as
       ` N ( x ) ` . $)
    isgrpd $p |- ( ph -> G e. Grp ) $=
      ( cv wcel wceq wa co wrex oveq1 eqeq1d rcla4ev syl2anc isgrpde ) ABCDEFGI
      JKLMNOABRZESUAHESHUIFUBZITZCRZUIFUBZITZCEUCPQUNUKCHEULHTUMUJIULHUIFUDUEUF
      UGUH $.
      $( [6-Jan-2015] $) $( [6-Jun-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d y N $.  $d x y z P $.  $d x y z Z $.
    isgrpi.b $e |- B = ( Base ` G ) $.
    isgrpi.p $e |- P = ( +g ` G ) $.
    isgrpi.c $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpi.a $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpi.z $e |- Z e. B $.
    isgrpi.i $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpi.n $e |- ( x e. B -> N e. B ) $.
    isgrpi.j $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpi $p |- G e. Grp $=
      ( wcel wtru wceq co cgrp cbs cfv a1i cplusg cv 3adant1 adantl isgrpd trud
      w3a ) FUAQRABCDEFGHDFUBUCSRIUDEFUEUCSRJUDAUFZDQZBUFZDQZULUNETZDQRKUGUMUOC
      UFZDQUKUPUQETULUNUQETETSRLUHHDQRMUDUMHULETULSRNUHUMGDQROUHUMGULETHSRPUHUI
      UJ $.
      $( [3-Sep-2011] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d y N $.  $d x y z P $.  $d x y z Z $.
    isgrpix.a $e |- B e. _V $.
    isgrpix.b $e |- P e. _V $.
    isgrpix.g $e |- G = { <. 1 , B >. , <. 2 , P >. } $.
    isgrpix.2 $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpix.3 $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpix.z $e |- Z e. B $.
    isgrpix.5 $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpix.6 $e |- ( x e. B -> N e. B ) $.
    isgrpix.7 $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group.  Read ` N ` as ` N ( x ) ` .  Note:
       This theorem has hard-coded structure indices for demonstration
       purposes.  It is not intended for general use. $)
    isgrpix $p |- G e. Grp $=
      ( grpbasex grpplusgx isgrpi ) ABCDEFGHDEFIJKRDEFIJKSLMNOPQT $.
      $( [4-Sep-2011] $)
  $}

  ${
    grpidcl.b $e |- B = ( Base ` G ) $.
    grpidcl.o $e |- O = ( 0g ` G ) $.
    $( The identity element of a group belongs to the group. $)
    grpidcl $p |- ( G e. Grp -> O e. B ) $=
      ( cgrp wcel cmnd grpmnd mndidcl syl ) BFGBHGCAGBIABCDEJK $.
      $( [27-Dec-2014] $) $( [27-Aug-2011] $)
  $}

  ${
    grpbn0.b $e |- B = ( Base ` G ) $.
    $( The base set of a group is not empty.  (Contributed by Szymon
       Jaroszewicz, 3-Apr-2007.) $)
    grpbn0 $p |- ( G e. Grp -> B =/= (/) ) $=
      ( cgrp wcel c0g cfv c0 wne eqid grpidcl ne0i syl ) BDEBFGZAEAHIABNCNJKANL
      M $.
      $( [18-Oct-2012] $)

    grplid.p $e |- P = ( +g ` G ) $.
    grplid.o $e |- O = ( 0g ` G ) $.
    $( The identity element of a group is a left identity. $)
    grplid $p |- ( ( G e. Grp /\ X e. B ) -> ( O P X ) = X ) $=
      ( cgrp wcel cmnd co wceq grpmnd mndlid sylan ) CIJCKJEAJDEBLEMCNABCDEFGHO
      P $.
      $( [18-Aug-2011] $)

    $( The identity element of a group is a right identity. $)
    grprid $p |- ( ( G e. Grp /\ X e. B ) -> ( X P O ) = X ) $=
      ( cgrp wcel cmnd co wceq grpmnd mndrid sylan ) CIJCKJEAJEDBLEMCNABCDEFGHO
      P $.
      $( [18-Aug-2011] $)
  $}

  $( A group is not empty.  (Contributed by Mario Carneiro, 2-Dec-2014.) $)
  grpn0 $p |- ( G e. Grp -> G =/= (/) ) $=
    ( vx cgrp wcel cbs cfv c0 wne eqid grpbn0 wceq fveq2 baseid syl6eqr necon3i
    cnx str0 syl ) ACDAEFZGHAGHSASIJAGSGAGKSGEFGAGELEPEFBBMQNOR $.
    $( [2-Dec-2014] $)

  ${
    $d u v w y B $.  $d u v w y G $.  $d u v w y P $.  $d u v w y X $.
    $d u v w y Y $.  $d u v w y Z $.
    grprcan.b $e |- B = ( Base ` G ) $.
    grprcan.p $e |- P = ( +g ` G ) $.
    $( Right cancellation law for groups.  (The proof was shortened by Mario
       Carneiro, 6-Jan-2015.) $)
    grprcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( X P Z ) = ( Y P Z ) <-> X = Y ) ) $=
      ( vy vu vv vw wcel w3a wa co wceq cv sylan 3eqtr3d cgrp c0g cfv wrex eqid
      wi grpinvex 3ad2antr3 simprr oveq1d simpll grpass simplr1 simplr3 simprll
      caovassd simplr2 idi grpcl syl3an1 grpidcl grplid adantr simprlr grprinvd
      syl simpr mpdan oveq2d grprid syl2anc expr rexlimdva mpd oveq1 impbid1 )
      CUAMZDAMZEAMZFAMZNZOZDFBPZEFBPZQZDEQZWBIRZFBPCUBUCZQZIAUDZWEWFUFZVQVRVTWJ
      VSIABCFWHGHWHUEZUGUHWBWIWKIAWBWGAMZWIWKWBWMWIOZWEWFWBWNWEOZOZDWHBPZEWHBPZ
      DEWPDFWGBPZBPZEWSBPZWQWRWPWCWGBPWDWGBPWTXAWPWCWDWGBWBWNWEUIUJWPJKLDFWGABW
      PVQJRZAMZKRZAMZLRZAMNXBXDBPZXFBPXBXDXFBPBPQVQWAWOUKZABCXBXDXFGHULSZVRVSVT
      VQWOUMZVRVSVTVQWOUNZWBWMWIWEUOZUPWPJKLEFWGABXIVRVSVTVQWOUQZXKXLUPTWPWSWHD
      BWPVTWSWHQWPVTUFXKURWPVTJKLABWGWHFWPVQXCXEXGAMXHABCXBXDGHUSUTWPVQWHAMXHAC
      WHGWLVAVFWPVQXCWHXBBPXBQXHABCWHXBGHWLVBSXIWPVQXCXDXBBPWHQKAUDXHKABCXBWHGH
      WLUGSWPVTVGWPWMVTXLVCWPWIVTWBWMWIWEVDVCVEVHZVIWPWSWHEBXNVITWPVQVRWQDQXHXJ
      ABCWHDGHWLVJVKWPVQVSWREQXHXMABCWHEGHWLVJVKTVLVLVMVNDEFBVOVP $.
      $( [6-Jan-2015] $) $( [24-Aug-2011] $)
  $}

  ${
    $d y z B $.  $d y z G $.  $d y z P $.  $d y z O $.  $d y z X $.
    grpinveu.b $e |- B = ( Base ` G ) $.
    grpinveu.p $e |- P = ( +g ` G ) $.
    grpinveu.o $e |- O = ( 0g ` G ) $.
    $( The left inverse element of a group is unique.  Lemma 2.2.1(b) of
       [Herstein] p. 55. $)
    grpinveu $p |- ( ( G e. Grp /\ X e. B )
           -> E! y e. B ( y P X ) = O ) $=
      ( vz cgrp wcel wa cv co wceq wi wral wrex wreu grpinvex w3a eqtr3 grprcan
      syl5ib 3exp2 com24 imp41 an32s exp3a ralrimdva ancld reximdva eqeq1d reu8
      mpd oveq1 sylibr ) DKLZFBLZMZANZFCOZEPZJNZFCOZEPZVBVEPZQZJBRZMZABSZVDABTV
      AVDABSVLABCDFEGHIUAVAVDVKABVAVBBLZMZVDVJVNVDVIJBVNVEBLZMVDVGVHVAVOVMVDVGM
      ZVHQZUSUTVOVMVQUSVMVOUTVQUSVMVOUTVQVPVCVFPUSVMVOUTUBMVHVCVFEUCBCDVBVEFGHU
      DUEUFUGUHUIUJUKULUMUPVDVGAJBVHVCVFEVBVEFCUQUNUOUR $.
      $( [24-Aug-2011] $)

    $( Two ways of saying that an element of a group is the identity element.
       Provides a convenient way to compute the value of the identity
       element. $)
    grpid $p |- ( ( G e. Grp /\ X e. B ) ->
                  ( ( X P X ) = X <-> O = X ) ) $=
      ( wceq cgrp wcel wa co eqcom wb wi grpidcl grprcan 3exp2 mpid pm2.43d imp
      grplid eqeq2d bitr3d syl5rbb ) DEIEDIZCJKZEAKZLZEEBMZEIZDENUJUKDEBMZIZUGU
      LUHUIUNUGOZUHUIUOUHUIDAKZUIUOPACDFHQUHUIUPUIUOABCEDEFGRSTUAUBUJUMEUKABCDE
      FGHUCUDUEUF $.
      $( [24-Aug-2011] $)

    $( Obsolete proof of ~ grpid as of 9-Mar-2014. $)
    grpidOLD $p |- ( ( G e. Grp /\ X e. B ) ->
                  ( X = O <-> ( X P X ) = X ) ) $=
      ( cgrp wcel wa co wceq wb wi grpidcl grprcan 3exp2 mpid pm2.43d grplid
      imp eqeq2d bitr3d ) CIJZEAJZKZEEBLZDEBLZMZEDMZUHEMUEUFUJUKNZUEUFULUEUFDAJ
      ZUFULOACDFHPUEUFUMUFULABCEDEFGQRSTUBUGUIEUHABCDEFGHUAUCUD $.
      $( [24-Aug-2011] $)

    $( Properties showing that an element ` Z ` is the identity element of a
       group. $)
    isgrpid2 $p |- ( G e. Grp
  -> ( ( Z e. B /\ ( Z P Z ) = Z ) <-> O = Z ) ) $=
      ( cgrp wcel co wceq wa grpid biimpd expimpd grpidcl grplid mpdan jca id
      eleq1 oveq12d eqeq12d anbi12d syl5ibcom impbid ) CIJZEAJZEEBKZELZMZDELZUH
      UIUKUMUHUIMUKUMABCDEFGHNOPUHDAJZDDBKZDLZMUMULUHUNUPACDFHQZUHUNUPUQABCDDFG
      HRSTUMUNUIUPUKDEAUBUMUOUJDEUMDEDEBUMUAZURUCURUDUEUFUG $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g x O $.  $d g x P $.  $d x y X $.
    grpinvval.b $e |- B = ( Base ` G ) $.
    grpinvval.p $e |- P = ( +g ` G ) $.
    grpinvval.o $e |- O = ( 0g ` G ) $.
    grpinvval.n $e |- N = ( invg ` G ) $.
    $( The inverse function of a group. $)
    grpinvfval $p |- ( G e. V
              -> N = ( x e. B |-> ( iota_ y e. B ( y P x ) = O ) ) ) $=
      ( vg cvv cv wceq cfv cbs cplusg c0g wcel co crio cmpt cminusg fveq2 oveqd
      elex syl6eqr eqeq12d riotaeqbidv mpteq12dv df-minusg eqeltri mptex syl5eq
      fvex fvmpt syl ) EHUAENUAZFACBOZAOZDUBZGPZBCUCZUDZPEHUHUTFEUEQVFLMEAMOZRQ
      ZVAVBVGSQZUBZVGTQZPZBVHUCZUDVFNUEVGEPZAVHVMCVEVNVHERQZCVGERUFIUIZVNVLVDBV
      HCVPVNVJVCVKGVNVIDVAVBVNVIESQDVGESUFJUIUGVNVKETQGVGETUFKUIUJUKULABMUMACVE
      CVONIERUQUNUOURUPUS $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)

    $( The inverse of a group element. $)
    grpinvval $p |- ( ( G e. V /\ X e. B ) ->
                    ( N ` X ) = ( iota_ y e. B ( y P X ) = O ) ) $=
      ( vx wcel cfv cv co wceq crio cmpt grpinvfval fveq1d oveq2 riotabidv eqid
      eqeq1d riotaex fvmpt sylan9eq ) DGNZHBNHEOHMBAPZMPZCQZFRZABSZTZOUKHCQZFRZ
      ABSZUJHEUPMABCDEFGIJKLUAUBMHUOUSBUPULHRZUNURABUTUMUQFULHUKCUCUFUDUPUEURAB
      UGUHUI $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g x y I $.  $d g x y P $.  $d x y X $.
    $d x y Y $.
    grpsubval.b $e |- B = ( Base ` G ) $.
    grpsubval.p $e |- P = ( +g ` G ) $.
    grpsubval.i $e |- I = ( invg ` G ) $.
    grpsubval.m $e |- M = ( -g ` G ) $.
    $( Group subtraction (division) operation. $)
    grpsubfval $p |- ( G e. V
              -> M = ( x e. B , y e. B |-> ( x P ( I ` y ) ) ) ) $=
      ( vg cvv cv cfv cbs cminusg cplusg fveq2 wcel cmpt2 wceq elex csg syl6eqr
      eqidd fveq1d oveq123d mpt2eq123dv df-sbg fvex eqeltri mpt2ex fvmpt syl5eq
      co syl ) EHUAENUAZGABCCAOZBOZFPZDUQZUBZUCEHUDUSGEUEPVDLMEABMOZQPZVFUTVAVE
      RPZPZVESPZUQZUBVDNUEVEEUCZABVFVFVJCCVCVKVFEQPZCVEEQTIUFZVMVKUTUTVHVBVIDVK
      VIESPDVEESTJUFVKUTUGVKVAVGFVKVGERPFVEERTKUFUHUIUJABMUKABCCVCCVLNIEQULUMZV
      NUNUOUPUR $.
      $( [31-Mar-2014] $)

    $( Group subtraction (division) operation. $)
    grpsubval $p |- ( ( X e. B /\ Y e. B ) ->
                    ( X M Y ) = ( X P ( I ` Y ) ) ) $=
      ( vx vy wcel wa co cv cfv cbs wceq cmpt2 cdm elfvdm eleq2s grpsubfval syl
      adantr oveqd oveq1 fveq2 oveq2d eqid ovex ovmpt2 eqtrd ) FANZGANZOZFGEPFG
      LMAALQZMQZDRZBPZUAZPFGDRZBPZUREVCFGURCSUBZNZEVCTUPVGUQVGFCSRAFCSUCHUDUGLM
      ABCDEVFHIJKUEUFUHLMFGAAVBVEVCFVABPUSFVABUIUTGTVAVDFBUTGDUJUKVCULFVDBUMUNU
      O $.
      $( [13-Dec-2014] $) $( [31-Mar-2014] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y X $.
    grpinvcl.b $e |- B = ( Base ` G ) $.
    grpinvcl.n $e |- N = ( invg ` G ) $.
    $( A group element's inverse is a group element. $)
    grpinvcl $p |- ( ( G e. Grp /\ X e. B ) -> ( N ` X ) e. B ) $=
      ( vy cgrp wcel wa cfv cv cplusg co c0g wceq crio eqid grpinvval wreu syl
      grpinveu riotacl eqeltrd ) BHIDAIJZDCKGLDBMKZNBOKZPZGAQZAGAUFBCUGHDEUFRZU
      GRZFSUEUHGATUIAIGAUFBUGDEUJUKUBUHGAUCUAUD $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d e w x y z B $.  $d e x y z G $.  $d e w x y z O $.  $d e w x y z P $.
    $d e w x M $.  $d x y z N $.  $d y z X $.
    grpinv.b $e |- B = ( Base ` G ) $.
    grpinv.p $e |- P = ( +g ` G ) $.
    grpinv.u $e |- O = ( 0g ` G ) $.
    grpinv.n $e |- N = ( invg ` G ) $.
    $( The left inverse of a group element. $)
    grplinv $p |- ( ( G e. Grp /\ X e. B ) -> ( ( N ` X ) P X ) = O ) $=
      ( vy cgrp wcel wa cfv cv co wceq crab syl crio grpinvval grpinveu eqeltrd
      wreu riotacl2 oveq1 eqeq1d elrab simprbi ) CLMFAMNZFDOZKPZFBQZERZKASZMZUL
      FBQZERZUKULUOKAUAZUPKABCDELFGHIJUBUKUOKAUEUTUPMKABCEFGHIUCUOKAUFTUDUQULAM
      USUOUSKULAUMULRUNUREUMULFBUGUHUIUJT $.
      $( [6-Jan-2015] $) $( [24-Aug-2011] $)

    $( The right inverse of a group element. $)
    grprinv $p |- ( ( G e. Grp /\ X e. B ) -> ( X P ( N ` X ) ) = O ) $=
      ( vx vy vz cgrp wcel cfv cv grpcl grpidcl grplid grpass grpinvex grpinvcl
      simpr grplinv grprinvd ) CNOZFAOZKLMABFDPEFABCKQZLQZGHRACEGISABCEUIGHITAB
      CUIUJMQGHUALABCUIEGHIUBUGUHUDACDFGJUCABCDEFGHIJUEUF $.
      $( [6-Jan-2015] $) $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid1 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( X P Y ) = O ) ) $=
      ( wcel w3a wceq co wa oveq2 adantl 3adant3 adantr cgrp cfv grprinv eqtr3d
      grplinv oveq1d grpinvcl adantrr simprl simprr grpass syldan 3impb 3adant2
      3jca grplid 3eqtr3d grprid 3eqtr3rd impbida ) CUALZFALZGALZMZFDUBZGNZFGBO
      ZENZVDVFPFVEBOZVGEVFVIVGNVDVEGFBQRVDVIENZVFVAVBVJVCABCDEFHIJKUCSTUDVDVHPV
      EVGBOZVEEBOZGVEVHVKVLNVDVGEVEBQRVDVKGNVHVDVEFBOZGBOZEGBOZVKGVAVBVNVONVCVA
      VBPVMEGBABCDEFHIJKUEUFSVAVBVCVNVKNZVAVBVCPZVEALZVBVCMVPVAVQPVRVBVCVAVBVRV
      CACDFHKUGZUHVAVBVCUIVAVBVCUJUOABCVEFGHIUKULUMVAVCVOGNVBABCEGHIJUPUNUQTVDV
      LVENZVHVAVBVTVCVAVBVRVTVSABCEVEHIJURULSTUSUT $.
      $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid2 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( Y P X ) = O ) ) $=
      ( wcel w3a wceq co wa oveq1 adantl 3adant3 adantr cgrp cfv grplinv eqtr3d
      grpinvcl grplid syldan eqcomd simprr simprl adantrr grpass grprinv oveq2d
      3jca 3impb grprid 3adant2 3eqtrd 3eqtr2d impbida ) CUALZFALZGALZMZFDUBZGN
      ZGFBOZENZVEVGPVFFBOZVHEVGVJVHNVEVFGFBQRVEVJENZVGVBVCVKVDABCDEFHIJKUCSTUDV
      EVIPVFEVFBOZVHVFBOZGVEVFVLNVIVEVLVFVBVCVLVFNZVDVBVCVFALZVNACDFHKUEZABCEVF
      HIJUFUGSUHTVIVMVLNVEVHEVFBQRVEVMGNVIVEVMGFVFBOZBOZGEBOZGVBVCVDVMVRNZVBVCV
      DPZVDVCVOMVTVBWAPVDVCVOVBVCVDUIVBVCVDUJVBVCVOVDVPUKUOABCGFVFHIULUGUPVBVCV
      RVSNVDVBVCPVQEGBABCDEFHIJKUMUNSVBVDVSGNVCABCEGHIJUQURUSTUTVA $.
      $( [24-Aug-2011] $)

    $( Properties showing that a function ` M ` is the inverse function of a
       group. $)
    isgrpinv $p |- ( G e. Grp
  -> ( ( M : B --> B /\ A. x e. B ( ( M ` x ) P x ) = O ) <-> N = M ) ) $=
      ( ve cgrp wcel wf cv co wceq wa syl2anc vw cfv wral crio simplr grpinvval
      simplll simpr wreu wb simpllr ffvelrn grpinveu ax-17 oveq1 eqeq1d riota2f
      a17d mpbid eqtrd ralimdva impr wfn cmpt cvv eqid fnmpt riotaex grpinvfval
      ex a1i fneq1d adantr mpbiri ffn ad2antrl eqfnfv2 mpbird grpinvcl eqeltrrd
      mprg fmptd feq1d grplinv ralrimiva fveq1 oveq1d ralbidv anbi12d syl5ibcom
      jca feq1 impbid ) DMNZBBEOZAPZEUBZWPCQZGRZABUCZSZFERZWNXAXBWNXASZXBWPFUBZ
      WQRZABUCZWNWOWTXFWNWOSZWSXEABXGWPBNZSZWSXEXIWSSZXDLPZWPCQZGRZLBUDZWQXJWNX
      HXDXNRWNWOXHWSUGZXGXHWSUEZLBCDFGMWPHIJKUFZTXJWSXNWQRZXIWSUHXJWQBNZXMLBUIZ
      WSXRUJXJWOXHXSWNWOXHWSUKXPBBWPEULTXJWNXHXTXOXPLBCDGWPHIJUMTXMWSLUABWQUAPW
      QNLUNXSWSLURXKWQRXLWRGXKWQWPCUOUPUQTUSUTVJVAVBXCFBVCZEBVCZXBXFUJXCYAABXNV
      DZBVCZXNVENZYDABABXNYCVEYCVFZVGYEXHXMLBVHVKWAWNYAYDUJXAWNBFYCALBCDFGMHIJK
      VIZVLVMVNWOYBWNWTBBEVOVPABFEVQTVRVJWNBBFOZXDWPCQZGRZABUCZSXBXAWNYHYKWNYHB
      BYCOWNABXNBYCWNXHSXDXNBXQBDFWPHKVSVTYFWBWNBBFYCYGWCVRWNYJABBCDFGWPHIJKWDW
      EWKXBYHWOYKWTBBFEWLXBYJWSABXBYIWRGXBXDWQWPCWPFEWFWGUPWHWIWJWM $.
      $( [7-Aug-2013] $)
  $}

  ${
    grpinvid.u $e |- O = ( 0g ` G ) $.
    grpinvid.n $e |- N = ( invg ` G ) $.
    $( The inverse of the identity element of a group. $)
    grpinvid $p |- ( G e. Grp -> ( N ` O ) = O ) $=
      ( cgrp wcel cfv wceq cplusg co cbs eqid grpidcl grplid mpdan wb grpinvid1
      mpd3an23 mpbird ) AFGZCBHCIZCCAJHZKCIZUACALHZGZUDUEACUEMZDNZUEUCACCUGUCMZ
      DOPUAUFUFUBUDQUHUHUEUCABCCCUGUIDERST $.
      $( [24-Aug-2011] $)
  $}

  ${
    grplcan.b $e |- B = ( Base ` G ) $.
    grplcan.p $e |- P = ( +g ` G ) $.
    $( Left cancellation law for groups. $)
    grplcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( Z P X ) = ( Z P Y ) <-> X = Y ) ) $=
      ( wcel w3a wa co wceq cfv oveq2 eqid adantlr adantrl adantr 3eqtr3d exp53
      cgrp cminusg adantl c0g grplinv oveq1d grpinvcl simprr simprl 3jca grpass
      wi syldan anassrs grplid adantrr 3imp2 impbid1 ) CUBIZDAIZEAIZFAIZJKFDBLZ
      FEBLZMZDEMZUTVAVBVCVFVGUMUTVAVBVCVFVGUTVAKZVBVCKZKZVFKFCUCNZNZVDBLZVLVEBL
      ZDEVFVMVNMVJVDVEVLBOUDVJVMDMZVFVHVCVOVBVHVCKZVLFBLZDBLZCUENZDBLZVMDVPVQVS
      DBUTVCVQVSMZVAABCVKVSFGHVSPZVKPZUFZQUGUTVAVCVRVMMZUTVAVCKZVLAIZVCVAJWEUTW
      FKWGVCVAUTVCWGVAACVKFGWCUHZRUTVAVCUIUTVAVCUJUKABCVLFDGHULUNUOVHVTDMVCABCV
      SDGHWBUPSTRSVJVNEMZVFUTVIWIVAUTVIKZVQEBLZVSEBLZVNEWJVQVSEBUTVCWAVBWDRUGUT
      VIWGVCVBJWKVNMWJWGVCVBUTVCWGVBWHRUTVBVCUIUTVBVCUJUKABCVLFEGHULUNUTVBWLEMV
      CABCVSEGHWBUPUQTQSTUAURDEFBOUS $.
      $( [25-Aug-2011] $)
  $}

  ${
    grpinvinv.b $e |- B = ( Base ` G ) $.
    grpinvinv.n $e |- N = ( invg ` G ) $.
    $( Double inverse law for groups.  Lemma 2.2.1(c) of [Herstein] p. 55. $)
    grpinvinv $p |- ( ( G e. Grp /\ X e. B ) -> ( N ` ( N ` X ) ) = X ) $=
      ( cgrp wcel wa cfv cplusg co wceq c0g grpinvcl eqid grprinv syldan eqtr4d
      grplinv wb simpl simpr grplcan syl13anc mpbid ) BGHZDAHZIZDCJZUJCJZBKJZLZ
      UJDULLZMZUKDMZUIUMBNJZUNUGUHUJAHZUMUQMABCDEFOZAULBCUQUJEULPZUQPZFQRAULBCU
      QDEUTVAFTSUIUGUKAHZUHURUOUPUAUGUHUBUGUHURVBUSABCUJEFORUGUHUCUSAULBUKDUJEU
      TUDUEUF $.
      $( [31-Mar-2014] $)

    $d x y B $.  $d x y G $.  $d x y N $.  $d x y ph $.
    grpinvf1o.g $e |- ( ph -> G e. Grp ) $.
    $( The group inverse is a one-to-one onto function. $)
    grpinvf1o $p |- ( ph -> N : B -1-1-onto-> B ) $=
      ( vx vy wfn wceq cv cfv wral cvv wcel eqid cgrp wa sylan crn wi cplusg co
      weq wf1o c0g crio cmpt riotaex rgenw fnmpt ax-mp grpinvfval fneq1d mpbiri
      wrex wb fvelrnb grpinvcl eleq1 syl5ibcom rexlimdva grpinvinv fveq2 eqeq1d
      syl rcla4ev syl2anc impbid bitrd eqrdv adantrr adantrl eqeq12d ralrimivva
      ex syl5ib dff1o6 syl3anbrc ) ADBJZDUAZBKHLZDMZILZDMZKZHIUEZUBZIBNHBNBBDUF
      AWAHBWEWCCUCMZUDCUGMZKZIBUHZUIZBJZWMOPZHBNWOWPHBWLIBUJUKHBWMWNOWNQULUMABD
      WNACRPZDWNKGHIBWJCDWKREWJQWKQFUNVGUOUPZAIWBBAWEWBPZWDWEKZHBUQZWEBPZAWAWSX
      AURWRHBWEDUSVGAXAXBAWTXBHBAWCBPZSWDBPZWTXBAWQXCXDGBCDWCEFUTTWDWEBVAVBVCAX
      BXAAWQXBXAGWQXBSWFBPWFDMZWEKZXABCDWEEFUTBCDWEEFVDZWTXFHWFBWCWFKWDXEWEWCWF
      DVEVFVHVITVQVJVKVLAWIHIBBWGWDDMZXEKAXCXBSSZWHWDWFDVEXIXHWCXEWEAXCXHWCKZXB
      AWQXCXJGBCDWCEFVDTVMAXBXFXCAWQXBXFGXGTVNVOVRVPHIBBDVSVT $.
      $( [22-Oct-2014] $)
  $}

  ${
    $d w x y z B $.  $d w x y z K $.  $d w x y z L $.  $d w x y z ph $.
    grpinvpropd.1 $e |- ( ph -> B = ( Base ` K ) ) $.
    grpinvpropd.2 $e |- ( ph -> B = ( Base ` L ) ) $.
    grpinvpropd.3 $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
      ( x ( +g ` K ) y ) = ( x ( +g ` L ) y ) ) $.
    grpinvpropd.4 $e |- ( ph -> K e. _V ) $.
    grpinvpropd.5 $e |- ( ph -> L e. _V ) $.
    $( If two structures have the same group components (properties), they have
       the same group inversion function. $)
    grpinvpropd $p |- ( ph -> ( invg ` K ) = ( invg ` L ) ) $=
      ( cbs cfv wceq crio cmpt wcel wa cvv eqid cv cplusg co cminusg grpidpropd
      c0g adantr eqeq12d anassrs an32s riotabidva mpteq2dva riotaeqdv mpteq12dv
      wb 3eqtr3d grpinvfval syl 3eqtr4d ) ACELMZBUAZCUAZEUBMZUCZEUFMZNZBUTOZPZC
      FLMZVAVBFUBMZUCZFUFMZNZBVIOZPZEUDMZFUDMZACDVFBDOZPCDVMBDOZPVHVOACDVRVSAVB
      DQZRVFVMBDAVADQZVTVFVMUOZAWAVTWBAWAVTRZRVDVKVEVLIAVEVLNWCABCDEFGHIJKUEUGU
      HUIUJUKULACDVRUTVGGAVFBDUTGUMUNACDVSVIVNHAVMBDVIHUMUNUPAESQVPVHNJCBUTVCEV
      PVESUTTVCTVETVPTUQURAFSQVQVONKCBVIVJFVQVLSVITVJTVLTVQTUQURUS $.
      $( [27-Nov-2014] $)
  $}

  ${
    grpinvadd.b $e |- B = ( Base ` G ) $.
    grpinvadd.p $e |- P = ( +g ` G ) $.
    grpinvadd.n $e |- N = ( invg ` G ) $.
    $( The inverse of the group operation reverses the arguments.  Lemma
       2.2.1(d) of [Herstein] p. 55. $)
    grpinvadd $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                   ( N ` ( X P Y ) ) = ( ( N ` Y ) P ( N ` X ) ) ) $=
      ( wcel co cfv wceq grpinvcl 3adant2 3adant3 grpcl syl3anc grpass syl13anc
      cgrp w3a c0g simp1 simp2 simp3 eqid grprinv oveq1d grplid syl2anc 3eqtr3d
      oveq2d 3eqtrd wb grpinvid1 mpbird ) CUAJZEAJZFAJZUBZEFBKZDLFDLZEDLZBKZMZV
      BVEBKZCUCLZMZVAVGEFVEBKZBKZEVDBKZVHVAURUSUTVEAJZVGVKMURUSUTUDZURUSUTUEURU
      SUTUFZVAURVCAJZVDAJZVMVNURUTVPUSACDFGINOZURUSVQUTACDEGINPZABCVCVDGHQRZABC
      EFVEGHSTVAVJVDEBVAFVCBKZVDBKZVHVDBKZVJVDVAWAVHVDBURUTWAVHMUSABCDVHFGHVHUG
      ZIUHOUIVAURUTVPVQWBVJMVNVOVRVSABCFVCVDGHSTVAURVQWCVDMVNVSABCVHVDGHWDUJUKU
      LUMURUSVLVHMUTABCDVHEGHWDIUHPUNVAURVBAJVMVFVIUOVNABCEFGHQVTABCDVHVBVEGHWD
      IUPRUQ $.
      $( [27-Oct-2006] $)
  $}

  ${
    $d x y B $.  $d x y G $.
    grpsubcl.b $e |- B = ( Base ` G ) $.
    grpsubcl.m $e |- M = ( -g ` G ) $.
    $( Functionality of group subtraction. $)
    grpsubf $p |- ( G e. Grp -> M : ( B X. B ) --> B ) $=
      ( vx vy cgrp wcel cxp wf cv cminusg cfv cplusg cmpt2 wral eqid grpinvcl
      co 3adant2 grpcl syld3an3 3expb ralrimivva fmpt2 sylib grpsubfval mpbird
      feq1d ) BHIZAAJZACKULAFGAAFLZGLZBMNZNZBONZTZPZKZUKURAIZGAQFAQUTUKVAFGAAUK
      UMAIZUNAIZVAUKVBVCUPAIZVAUKVCVDVBABUOUNDUORZSUAAUQBUMUPDUQRZUBUCUDUEFGAAU
      RAUSUSRUFUGUKULACUSFGAUQBUOCHDVFVEEUHUJUI $.
      $( [9-Sep-2014] $)

    $( Closure of group subtraction. $)
    grpsubcl $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) -> ( X M Y ) e. B ) $=
      ( cgrp wcel cxp wf co grpsubf fovrn syl3an1 ) BHIAAJACKDAIEAIDECLAIABCFGM
      DEAAACNO $.
      $( [31-Mar-2014] $)

    $( Right cancellation law for group subtraction. $)
    grpsubrcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( X M Z ) = ( Y M Z ) <-> X = Y ) ) $=
      ( cgrp wcel w3a wa co wceq cminusg cfv cplusg wb eqid grpsubval 3ad2antr3
      3adant2 3adant1 eqeq12d adantl simpl simpr1 simpr2 grpinvcl grprcan bitrd
      syl13anc ) BIJZDAJZEAJZFAJZKZLZDFCMZEFCMZNZDFBOPZPZBQPZMZEVCVDMZNZDENZUQV
      AVGRUMUQUSVEUTVFUNUPUSVENUOAVDBVBCDFGVDSZVBSZHTUBUOUPUTVFNUNAVDBVBCEFGVIV
      JHTUCUDUEURUMUNUOVCAJZVGVHRUMUQUFUMUNUOUPUGUMUNUOUPUHUMUNUPVKUOABVBFGVJUI
      UAAVDBDEVCGVIUJULUK $.
      $( [31-Mar-2014] $)

    grpsubinv.m $e |- N = ( invg ` G ) $.
    $( Inverse of a group subtraction. $)
    grpsubinv $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                      ( N ` ( X M Y ) ) = ( Y M X ) ) $=
      ( cgrp wcel w3a cfv cplusg co wceq grpinvcl 3adant2 grpsubval 3adant1
      eqid grpinvadd syld3an3 grpinvinv oveq1d eqtrd fveq2d ancoms 3eqtr4d ) BJ
      KZEAKZFAKZLZEFDMZBNMZOZDMZFEDMZUOOZEFCOZDMFECOZUMUQUNDMZURUOOZUSUJUKULUNA
      KZUQVCPUJULVDUKABDFGIQRAUOBDEUNGUOUAZIUBUCUMVBFURUOUJULVBFPUKABDFGIUDRUEU
      FUMUTUPDUKULUTUPPUJAUOBDCEFGVEIHSTUGUKULVAUSPZUJULUKVFAUOBDCFEGVEIHSUHTUI
      $.
      $( [9-Sep-2014] $)
  $}

  ${
    grpsubid.b $e |- B = ( Base ` G ) $.
    grpsubid.o $e |- O = ( 0g ` G ) $.
    grpsubid.m $e |- M = ( -g ` G ) $.
    $( Subtraction of a group element from itself. $)
    grpsubid $p |- ( ( G e. Grp /\ X e. B ) -> ( X M X ) = O ) $=
      ( cgrp wcel wa co cminusg cfv cplusg wceq eqid grpsubval anidms adantl
      grprinv eqtrd ) BIJZEAJZKEECLZEEBMNZNBONZLZDUDUEUHPZUCUDUIAUGBUFCEEFUGQZU
      FQZHRSTAUGBUFDEFUJGUKUAUB $.
      $( [31-Mar-2014] $)

    $( If the difference between two group elements is zero, they are equal.
       ( ~ subeq0 analog.) $)
    grpsubeq0 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B )
                  -> ( ( X M Y ) = O <-> X = Y ) ) $=
      ( cgrp wcel w3a co wceq cminusg cfv cplusg eqid eqeq1d 3adant2 3adant1 wb
      grpsubval simp1 grpinvcl simp2 grpinvid2 syl3anc grpinvinv syl6bb 3bitr2d
      eqcom ) BJKZEAKZFAKZLZEFCMZDNEFBOPZPZBQPZMZDNZUSURPZENZEFNZUPUQVADUNUOUQV
      ANUMAUTBURCEFGUTRZURRZIUCUASUPUMUSAKZUNVDVBUBUMUNUOUDUMUOVHUNABURFGVGUETU
      MUNUOUFAUTBURDUSEGVFHVGUGUHUPVDFENVEUPVCFEUMUOVCFNUNABURFGVGUITSFEULUJUK
      $.
      $( [31-Mar-2014] $)
  $}

  ${
    grpsubadd.b $e |- B = ( Base ` G ) $.
    grpsubadd.p $e |- P = ( +g ` G ) $.
    grpsubadd.m $e |- M = ( -g ` G ) $.
    $( Relationship between group subtraction and addition. $)
    grpsubadd $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) )
          -> ( ( X M Y ) = Z <-> ( Z P Y ) = X ) ) $=
      ( cgrp wcel w3a co wceq cfv eqid eqeq1d 3ad2antr2 syl13anc cminusg adantl
      wa grpsubval 3adant3 wb simpl simpr1 grpinvcl grpcl syl3anc simpr3 simpr2
      grprcan c0g grpass grplinv oveq2d grprid 3ad2antr1 3eqtrd 3bitr2d syl6bb
      eqcom ) CKLZEALZFALZGALZMZUCZEFDNZGOZEGFBNZOZVMEOVJVLEFCUAPZPZBNZGOZVQFBN
      ZVMOZVNVJVKVQGVIVKVQOZVEVFVGWAVHABCVODEFHIVOQZJUDUEUBRVJVEVQALZVHVGVTVRUF
      VEVIUGZVJVEVFVPALZWCWDVEVFVGVHUHZVEVFVGWEVHACVOFHWBUISZABCEVPHIUJUKVEVFVG
      VHULVEVFVGVHUMZABCVQGFHIUNTVJVSEVMVJVSEVPFBNZBNZECUOPZBNZEVJVEVFWEVGVSWJO
      WDWFWGWHABCEVPFHIUPTVJWIWKEBVEVFVGWIWKOVHABCVOWKFHIWKQZWBUQSURVEVGVFWLEOV
      HABCWKEHIWMUSUTVARVBEVMVDVC $.
      $( [31-Mar-2014] $)

    $( Double group subtraction. $)
    grpsubsub $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( X M ( Y M Z ) ) = ( X P ( Z M Y ) ) ) $=
      ( cgrp wcel w3a wa co cminusg cfv wceq simpr1 3adant3r1 grpsubval syl2anc
      grpsubcl eqid grpsubinv oveq2d eqtrd ) CKLZEALZFALZGALZMNZEFGDOZDOZEUMCPQ
      ZQZBOZEGFDOZBOULUIUMALZUNUQRUHUIUJUKSUHUJUKUSUIACDFGHJUCTABCUODEUMHIUOUDZ
      JUAUBULUPUREBUHUJUKUPURRUIACDUOFGHJUTUETUFUG $.
      $( [2-Dec-2014] $) $( [24-Feb-2008] $)

    $( Associative-type law for group subtraction and addition. $)
    grpaddsubass $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X P Y ) M Z ) = ( X P ( Y M Z ) ) ) $=
      ( cgrp wcel w3a wa co cminusg cfv wceq grpsubval syl2anc simpr1 3ad2antr3
      simpl simpr2 eqid grpinvcl grpass syl13anc grpcl 3adant3r3 simpr3 3eqtr4d
      oveq2d ) CKLZEALZFALZGALZMZNZEFBOZGCPQZQZBOZEFVBBOZBOZUTGDOZEFGDOZBOUSUNU
      OUPVBALZVCVERUNURUCUNUOUPUQUAUNUOUPUQUDZUNUOUQVHUPACVAGHVAUEZUFUBABCEFVBH
      IUGUHUSUTALZUQVFVCRUNUOUPVKUQABCEFHIUIUJUNUOUPUQUKZABCVADUTGHIVJJSTUSVGVD
      EBUSUPUQVGVDRVIVLABCVADFGHIVJJSTUMUL $.
      $( [16-Apr-2014] $)

    $( Cancellation law for subtraction ( ~ pncan analog). $)
    grppncan $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
        ( ( X P Y ) M Y ) = X ) $=
      ( cgrp wcel w3a co c0g cfv wceq simp1 simp2 simp3 grpaddsubass wa 3adant2
      syl13anc eqid grpsubid oveq2d grprid 3adant3 3eqtrd ) CJKZEAKZFAKZLZEFBMF
      DMZEFFDMZBMZECNOZBMZEUMUJUKULULUNUPPUJUKULQUJUKULRUJUKULSZUSABCDEFFGHITUC
      UJULUPURPUKUJULUAUOUQEBACDUQFGUQUDZIUEUFUBUJUKUREPULABCUQEGHUTUGUHUI $.
      $( [16-Apr-2014] $)

    $( Cancellation law for subtraction ( ~ npcan analog). . $)
    grpnpcan $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
        ( ( X M Y ) P Y ) = X ) $=
      ( cgrp wcel w3a cminusg cfv co wceq eqid 3adant2 syld3an3 grpsubval grpcl
      grpinvcl syl2anc grppncan 3adant1 eqcomd grpinvinv oveq12d 3eqtr3rd ) CJK
      ZEAKZFAKZLZEFCMNZNZBOZUODOZUPUOUNNZBOZEEFDOZFBOUMUPAKZUOAKZUQUSPUJUKULVBV
      AUJULVBUKACUNFGUNQZUBRZABCEUOGHUASVDABCUNDUPUOGHVCITUCUJUKULVBUQEPVDABCDE
      UOGHIUDSUMUPUTURFBUMUTUPUKULUTUPPUJABCUNDEFGHVCITUEUFUJULURFPUKACUNFGVCUG
      RUHUI $.
      $( [19-Apr-2014] $)

    $( Double group subtraction ( ~ subsub4 analog).  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    grpsubsub4 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X M Y ) M Z ) = ( X M ( Z P Y ) ) ) $=
      ( cgrp wcel w3a wa co wceq grpsubcl grpnpcan syl3anc syl13anc simpl grpcl
      3adant3r3 simpr3 oveq1d simpr2 grpass simpr1 3eqtr3d wb grpsubadd mpbird
      eqcomd ) CKLZEALZFALZGALZMZNZEGFBOZDOZEFDOZGDOZUSVAVCPZVCUTBOZEPZUSVCGBOZ
      FBOZVBFBOZVEEUSVGVBFBUSUNVBALZUQVGVBPUNURUAZUNUOUPVJUQACDEFHJQUCZUNUOUPUQ
      UDZABCDVBGHIJRSUEUSUNVCALZUQUPVHVEPVKUSUNVJUQVNVKVLVMACDVBGHJQSZVMUNUOUPU
      QUFZABCVCGFHIUGTUSUNUOUPVIEPVKUNUOUPUQUHZVPABCDEFHIJRSUIUSUNUOUTALZVNVDVF
      UJVKVQUSUNUQUPVRVKVMVPABCGFHIUBSVOABCDEUTVCHIJUKTULUM $.
      $( [2-Dec-2014] $)

    $( Cancellation law for mixed addition and subtraction.  ( ~ pnpcan2
       analog.) $)
    grppnpcan2 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X P Z ) M ( Y P Z ) ) = ( X M Y ) ) $=
      ( cgrp wcel w3a wa co wceq simpl grpcl 3adant3r2 simpr3 simpr2 grpsubsub4
      syl13anc grppncan oveq1d eqtr3d ) CKLZEALZFALZGALZMZNZEGBOZGDOZFDOZUMFGBO
      DOZEFDOULUGUMALZUJUIUOUPPUGUKQUGUHUJUQUIABCEGHIRSUGUHUIUJTUGUHUIUJUAABCDU
      MGFHIJUBUCULUNEFDUGUHUJUNEPUIABCDEGHIJUDSUEUF $.
      $( [2-Dec-2014] $) $( [15-Feb-2008] $)

    $( Cancellation law for group subtraction.  ( ~ npncan analog.) $)
    grpnpncan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X M Y ) P ( Y M Z ) ) = ( X M Z ) ) $=
      ( cgrp wcel w3a wa co wceq simpl grpsubcl 3adant3r3 simpr2 simpr3 oveq1d
      grpaddsubass syl13anc grpnpcan eqtr3d ) CKLZEALZFALZGALZMZNZEFDOZFBOZGDOZ
      UMFGDOBOZEGDOULUGUMALZUIUJUOUPPUGUKQUGUHUIUQUJACDEFHJRSUGUHUIUJTUGUHUIUJU
      AABCDUMFGHIJUCUDULUNEGDUGUHUIUNEPUJABCDEFHIJUESUBUF $.
      $( [2-Dec-2014] $) $( [15-Feb-2008] $)
  $}

  ${
    grpnnncan2.b $e |- B = ( Base ` G ) $.
    grpnnncan2.m $e |- M = ( -g ` G ) $.
    $( Cancellation law for group subtraction.  ( ~ nnncan2 analog.) $)
    grpnnncan2 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X M Z ) M ( Y M Z ) ) = ( X M Y ) ) $=
      ( cgrp wcel w3a wa co cplusg cfv wceq simpl simpr1 simpr3 3adant3r1 eqtrd
      grpsubcl eqid grpsubsub4 syl13anc grpnpcan oveq2d ) BIJZDAJZEAJZFAJZKZLZD
      FCMEFCMZCMZDUNFBNOZMZCMZDECMUMUHUIUKUNAJZUOURPUHULQUHUIUJUKRUHUIUJUKSUHUJ
      UKUSUIABCEFGHUBTAUPBCDFUNGUPUCZHUDUEUMUQEDCUHUJUKUQEPUIAUPBCEFGUTHUFTUGUA
      $.
      $( [2-Dec-2014] $) $( [15-Feb-2008] $)
  $}

  ${
    $d n w x B $.  $d n s w x G $.  $d n s w x I $.  $d n x N $.  $d n w x O $.
    $d s w P $.  $d n x S $.  $d n x X $.
    mulgval.b $e |- B = ( Base ` G ) $.
    mulgval.p $e |- P = ( +g ` G ) $.
    mulgval.o $e |- O = ( 0g ` G ) $.
    mulgval.i $e |- I = ( invg ` G ) $.
    mulgval.t $e |- T = ( .g ` G ) $.
    $( Group multiple (exponentiation) operation. $)
    mulgfval $p |- ( G e. V -> T = ( n e. ZZ , x e. B |->
      if ( n = 0 , O , if ( 0 < n ,
        ( seq 1 ( P , ( NN X. { x } ) ) ` n ) ,
        ( I ` ( seq 1 ( P , ( NN X. { x } ) ) ` -u n ) ) ) ) ) ) $=
      ( vs cvv cz cv wceq cfv vw wcel cc0 clt wbr cn csn cxp c1 cseq cneg cmpt2
      cif cmg cbs c0g cplusg cminusg csb eqidd fveq2 syl6eqr biidd seqex a1i wa
      elex seqeq2d sylan9eqr fveq1d fveq2d fveq12d ifbieq12d csbied mpt2eq123dv
      id simpl df-mulg zex fvex eqeltri mpt2ex fvmpt syl5eq syl ) FIUBFPUBZDEAQ
      BERZUCSZHUCWGUDUEZWGCUFARUGUHZUIUJZTZWGUKZWKTZGTZUMZUMZULZSFIVGWFDFUNTWRN
      UAFEAQUARZUOTZWHWSUPTZOWSUQTZWJUIUJZWIWGORZTZWMXDTZWSURTZTZUMZUSZUMZULWRP
      UNWSFSZEAQWTXKQBWQXLQUTXLWTFUOTZBWSFUOVAJVBXLWHWHXAXJHWPXLWHVCXLXAFUPTHWS
      FUPVALVBXLOXCXIWPPXCPUBXLXBWJUIVDVEXLXDXCSZVFZWIWIXEXHWLWOXOWIVCXOWGXDWKX
      NXLXDXCWKXNVPXLXBCWJUIXLXBFUQTCWSFUQVAKVBVHVIZVJXOXFWNXGGXOXGFURTGXOWSFUR
      XLXNVQVKMVBXOWMXDWKXPVJVLVMVNVMVOAUAEOVREAQBWQVSBXMPJFUOVTWAWBWCWDWE $.
      $( [11-Dec-2014] $)

    mulgval.s $e |- S = seq 1 ( P , ( NN X. { X } ) ) $.
    $( Group multiple (exponentiation) operation. $)
    mulgval $p |- ( ( N e. ZZ /\ X e. B ) -> ( N T X ) =
      if ( N = 0 , O , if ( 0 < N , ( S ` N ) , ( I ` ( S ` -u N ) ) ) ) ) $=
      ( vn vx cc0 wceq cfv cz wcel wa co cv clt wbr cn csn cxp c1 cseq cneg cif
      cmpt2 cbs cdm elfvdm eleq2s adantl mulgfval oveqd syl simpl eqeq1d breq2d
      simpr sneqd xpeq2d seqeq3d syl6eqr fveq12d negeqd ifbieq12d ifbieq2d eqid
      fveq2d c0g cvv fvex eqeltri ifex ovmpt2a eqtrd ) GUAUBZIAUBZUCZGIDUDZGIPQ
      UAAPUEZRSZHRWIUFUGZWIBUHQUEZUIZUJZUKULZTZWIUMZWOTZFTZUNZUNZUOZUDZGRSZHRGU
      FUGZGCTZGUMZCTZFTZUNZUNZWGEUPUQZUBZWHXCSWFXMWEXMIEUPTAIEUPURJUSUTXMDXBGIQ
      ABDPEFHXLJKLMNVAVBVCPQGIUAAXAXKXBWIGSZWLISZUCZWJXDWTXJHXPWIGRXNXOVDZVEXPW
      KXEWPWSXFXIXPWIGRUFXQVFXPWIGWOCXPWOBUHIUIZUJZUKULCXPWNXSBUKXPWMXRUHXPWLIX
      NXOVGVHVIVJOVKZXQVLXPWRXHFXPWQXGWOCXTXPWIGXQVMVLVQVNVOXBVPXDHXJHEVRTVSLEV
      RVTWAXEXFXIGCVTXHFVTWBWBWCWD $.
      $( [11-Dec-2014] $)
  $}

  ${
    mulg0.b $e |- B = ( Base ` G ) $.
    mulg0.o $e |- O = ( 0g ` G ) $.
    mulg0.t $e |- T = ( .g ` G ) $.
    $( Group multiple (exponentiation) operation at zero. $)
    mulg0 $p |- ( X e. B -> ( 0 T X ) = O ) $=
      ( cc0 cz wcel co wceq 0z wa clt wbr cfv cif eqid cplusg csn cxp cseq cneg
      cn c1 cminusg mulgval iftrue ax-mp syl6eq mpan ) IJKZEAKZIEBLZDMNUNUOOUPI
      IMZDIIPQICUARZUFEUBUCUGUDZRIUEUSRCUHRZRSZSZDAURUSBCUTIDEFURTGUTTHUSTUIUQV
      BDMITUQDVAUJUKULUM $.
      $( [11-Dec-2014] $)
  $}

  ${
    mulgnn.b $e |- B = ( Base ` G ) $.
    mulgnn.p $e |- P = ( +g ` G ) $.
    mulgnn.t $e |- T = ( .g ` G ) $.
    mulgnn.s $e |- S = seq 1 ( P , ( NN X. { X } ) ) $.
    $( Group multiple (exponentiation) operation at a positive integer. $)
    mulgnn $p |- ( ( N e. NN /\ X e. B ) -> ( N T X ) = ( S ` N ) ) $=
      ( cn wcel cc0 wceq cfv cif eqid syl eqtrd wa c0g clt wbr cneg cminusg nnz
      co cz mulgval sylan wn wne nnne0 df-ne sylib iffalse nngt0 iftrue adantr
      ) FLMZGAMZUAFGDUHZFNOZEUBPZNFUCUDZFCPZFUECPEUFPZPZQZQZVGVAFUIMVBVCVKOFUGA
      BCDEVHFVEGHIVERVHRJKUJUKVAVKVGOVBVAVKVJVGVAVDULZVKVJOVAFNUMVLFUNFNUOUPVDV
      EVJUQSVAVFVJVGOFURVFVGVIUSSTUTT $.
      $( [11-Dec-2014] $)
  $}

  ${
    mulg1.b $e |- B = ( Base ` G ) $.
    mulg1.m $e |- T = ( .g ` G ) $.
    $( Group multiple (exponentiation) operation at one. $)
    mulg1 $p |- ( X e. B -> ( 1 T X ) = X ) $=
      ( wcel c1 co cplusg cfv cn csn cxp cseq wceq 1nn eqid mulgnn mpan mpan2
      1z fvconst2g seq1i eqtrd ) DAGZHDBIZHCJKZLDMNZHOZKZDHLGZUFUGUKPQAUHUJBCHD
      EUHRFUJRSTUFDUHUIHUBUFULHUIKDPQLDHAUCUAUDUE $.
      $( [11-Dec-2014] $)

    ${
      mulgnnp1.p $e |- P = ( +g ` G ) $.
      $( Group multiple (exponentiation) operation at a successor. $)
      mulgnnp1 $p |- ( ( N e. NN /\ X e. B ) ->
        ( ( N + 1 ) T X ) = ( ( N T X ) P X ) ) $=
        ( cn wcel wa c1 caddc co csn cxp cfv wceq mulgnn cseq cuz nnuz syl6eleq
        simpl seqp1 syl id peano2nn fvconst2g syl2anr oveq2d eqtrd sylan oveq1d
        eqid 3eqtr4d ) EJKZFAKZLZEMNOZBJFPQZMUAZRZEVCRZFBOZVAFCOZEFCOZFBOUTVDVE
        VAVBRZBOZVFUTEMUBRZKVDVJSUTEJVKURUSUEUCUDBVBMEUFUGUTVIFVEBUSUSVAJKZVIFS
        URUSUHEUIZJFVAAUJUKULUMURVLUSVGVDSVMABVCCDVAFGIHVCUPZTUNUTVHVEFBABVCCDE
        FGIHVNTUOUQ $.
        $( [11-Dec-2014] $)
    $}

    mulgnegnn.i $e |- I = ( invg ` G ) $.
    $( Group multiple (exponentiation) operation at a negative integer. $)
    mulgnegnn $p |- ( ( N e. NN /\ X e. B ) ->
      ( -u N T X ) = ( I ` ( N T X ) ) ) $=
      ( cn wcel cfv wceq syl fveq2d cc0 clt wbr eqid cr wa cneg cplusg csn cseq
      cxp c1 co cc nncn negneg adantr c0g cif cz nnnegz mulgval sylan wne nnne0
      wn wb negeq0 necon3abid mpbid iffalse renegcl nngt0 lt0neg2 wi 0re ltnsym
      nnre mpan2 sylc eqtrd mulgnn 3eqtr4d ) EJKZFAKZUAZEUBZUBZCUCLZJFUDUFUGUEZ
      LZDLZEWELZDLWBFBUHZEFBUHZDLWAWFWHDWAWCEWEVSWCEMZVTVSEUIKZWKEUJZEUKNULOOWA
      WIWBPMZCUMLZPWBQRZWBWELZWGUNZUNZWGVSWBUOKVTWIWSMEUPAWDWEBCDWBWOFGWDSZWOSI
      HWESZUQURVSWSWGMVTVSWSWRWGVSWNVAZWSWRMVSEPUSZXBEUTVSWLXCXBVBWMWLWNEPEVCVD
      NVEWNWOWRVFNVSWPVAZWRWGMVSWBTKZWBPQRZXDVSETKZXEEVMZEVGNVSPEQRZXFEVHVSXGXI
      XFVBXHEVINVEXEPTKXFXDVJVKWBPVLVNVOWPWQWGVFNVPULVPWAWJWHDAWDWEBCEFGWTHXAVQ
      OVR $.
      $( [11-Dec-2014] $)
  $}

  ${
    mulgnn0p1.b $e |- B = ( Base ` G ) $.
    mulgnn0p1.t $e |- T = ( .g ` G ) $.
    mulgnn0p1.p $e |- P = ( +g ` G ) $.
    $( Group multiple (exponentiation) operation at a successor, extended to
       ` NN0 ` . $)
    mulgnn0p1 $p |- ( ( G e. Mnd /\ N e. NN0 /\ X e. B ) ->
      ( ( N + 1 ) T X ) = ( ( N T X ) P X ) ) $=
      ( cmnd wcel cc0 wceq c1 caddc co wa adantl oveq1d oveq1 cn wo simp2 elnn0
      cn0 w3a sylib simpr simpl3 mulgnnp1 syl2anc c0g cfv mndlid mulg0 3eqtr4rd
      eqid mulg1 3adant2 0p1e1 syl6eqr eqeq12d syl5ibrcom imp jaodan mpdan ) DJ
      KZEUEKZFAKZUFZEUAKZELMZUBZENOPZFCPZEFCPZFBPZMZVJVHVMVGVHVIUCEUDUGVJVKVRVL
      VJVKQVKVIVRVJVKUHVGVHVIVKUIABCDEFGHIUJUKVJVLVRVJVRVLNFCPZLFCPZFBPZMZVGVIW
      BVHVGVIQZDULUMZFBPFWAVSABDWDFGIWDUQZUNWCVTWDFBVIVTWDMVGACDWDFGWEHUORSVIVS
      FMVGACDFGHURRUPUSVLVOVSVQWAVLVNNFCVLVNLNOPNELNOTUTVASVLVPVTFBELFCTSVBVCVD
      VEVF $.
      $( [11-Dec-2014] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d x y N $.  $d x y X $.
    mulgnncl.b $e |- B = ( Base ` G ) $.
    mulgnncl.t $e |- T = ( .g ` G ) $.
    $( Closure of the group multiple (exponentiation) operation.  TODO:  This
       can be generalized to a magma if/when we introduce them. $)
    mulgnncl $p |- ( ( G e. Mnd /\ N e. NN /\ X e. B ) -> ( N T X ) e. B ) $=
      ( vx vy cmnd wcel cn co cfv c1 wceq eqid cv wa eqeltrd w3a cplusg csn cxp
      cseq mulgnn 3adant1 cuz simp2 nnuz syl6eleq simp3 elfznn fvconst2g syl2an
      cfz simpl3 mndcl 3expb 3ad2antl1 seqcl ) CJKZDLKZEAKZUAZDEBMZDCUBNZLEUCUD
      ZOUEZNZAVCVDVFVJPVBAVGVIBCDEFVGQZGVIQUFUGVEHIVGAVHODVEDLOUHNVBVCVDUIUJUKV
      EHRZODUPMKZSVLVHNZEAVEVDVLLKVNEPVMVBVCVDULVLDUMLEVLAUNUOVBVCVDVMUQTVBVCVL
      AKZIRZAKZSVLVPVGMAKZVDVBVOVQVRAVGCVLVPFVKURUSUTVAT $.
      $( [11-Dec-2014] $)

    $( Closure of the group multiple (exponentiation) operation. $)
    mulgnn0cl $p |- ( ( G e. Mnd /\ N e. NN0 /\ X e. B ) -> ( N T X ) e. B ) $=
      ( cmnd wcel cn0 w3a cn cc0 wceq wo co simp2 elnn0 sylib wi 3com23 3adant2
      mulgnncl 3expia c0g cfv eqid mulg0 3ad2ant3 mndidcl 3ad2ant1 oveq1 eleq1d
      eqeltrd syl5ibrcom jaod mpd ) CHIZDJIZEAIZKZDLIZDMNZOZDEBPZAIZVAUSVDURUSU
      TQDRSVAVBVFVCURUTVBVFTUSURUTVBVFURVBUTVFABCDEFGUCUAUDUBVAVFVCMEBPZAIVAVGC
      UEUFZAUTURVGVHNUSABCVHEFVHUGZGUHUIURUSVHAIUTACVHFVIUJUKUNVCVEVGADMEBULUMU
      OUPUQ $.
      $( [11-Dec-2014] $)

    $( Closure of the group multiple (exponentiation) operation. $)
    mulgcl $p |- ( ( G e. Grp /\ N e. ZZ /\ X e. B ) -> ( N T X ) e. B ) $=
      ( cgrp wcel cz cneg wa co simpl1 syl simpl3 syl3anc cfv wceq syl2anc cmnd
      w3a cr cn wo simp2 elznn0nn sylib grpmnd simpr mulgnn0cl cminusg cc recnd
      cn0 simprl negneg oveq1d simprr mulgnegnn eqtr3d mulgnncl grpinvcl jaodan
      eqid eqeltrd mpdan ) CHIZDJIZEAIZUBZDUOIZDUCIZDKZUDIZLZUEZDEBMZAIZVKVIVQV
      HVIVJUFDUGUHVKVLVSVPVKVLLZCUAIZVLVJVSVTVHWAVHVIVJVLNCUIZOVKVLUJVHVIVJVLPA
      BCDEFGUKQVKVPLZVRVNEBMZCULRZRZAWCVNKZEBMZVRWFWCWGDEBWCDUMIWGDSWCDVKVMVOUP
      UNDUQOURWCVOVJWHWFSVKVMVOUSZVHVIVJVPPZABCWEVNEFGWEVEZUTTVAWCVHWDAIZWFAIVH
      VIVJVPNZWCWAVOVJWLWCVHWAWMWBOWIWJABCVNEFGVBQACWEWDFWKVCTVFVDVG $.
      $( [11-Dec-2014] $)

    mulgneg.i $e |- I = ( invg ` G ) $.
    $( Group multiple (exponentiation) operation at a negative integer. $)
    mulgneg $p |- ( ( G e. Grp /\ N e. ZZ /\ X e. B ) ->
      ( -u N T X ) = ( I ` ( N T X ) ) ) $=
      ( wcel cneg wa co cfv wceq cc0 simpl3 syl2anc syl oveq1d cz w3a cn0 cr cn
      cgrp simp2 elznn0nn sylib elnn0 simpr mulgnegnn c0g simpl1 grpinvid mulg0
      wo eqid eqtrd fveq2d negeqd neg0 syl6eq 3eqtr4rd jaodan simprr nnz mulgcl
      sylan2b syl3anc grpinvinv cc simprl recnd negneg eqtr3d mpdan ) CUFJZEUAJ
      ZFAJZUBZEUCJZEUDJZEKZUEJZLZUQZWDFBMZEFBMZDNZOZWAVSWGVRVSVTUGEUHUIWAWBWKWF
      WBWAEUEJZEPOZUQWKEUJWAWLWKWMWAWLLWLVTWKWAWLUKVRVSVTWLQABCDEFGHIULRWAWMLZC
      UMNZDNZWOWJWHWNVRWPWOOVRVSVTWMUNCDWOWOURZIUOSWNWIWODWNWIPFBMZWOWNEPFBWAWM
      UKZTWNVTWRWOOVRVSVTWMQABCWOFGWQHUPSZUSUTWNWHWRWOWNWDPFBWNWDPKPWNEPWSVAVBV
      CTWTUSVDVEVIWAWFLZWHDNZDNZWHWJXAVRWHAJZXCWHOVRVSVTWFUNZXAVRWDUAJZVTXDXEXA
      WEXFWAWCWEVFZWDVGSVRVSVTWFQZABCWDFGHVHVJACDWHGIVKRXAXBWIDXAWDKZFBMZXBWIXA
      WEVTXJXBOXGXHABCDWDFGHIULRXAXIEFBXAEVLJXIEOXAEWAWCWEVMVNEVOSTVPUTVPVEVQ
      $.
      $( [11-Dec-2014] $)

    $( Group multiple (exponentiation) operation at negative one. $)
    mulgm1 $p |- ( ( G e. Grp /\ X e. B ) -> ( -u 1 T X ) = ( I ` X ) ) $=
      ( cgrp wcel wa c1 cneg co cfv cz wceq 1z mulgneg mp3an2 adantl fveq2d
      mulg1 eqtrd ) CIJZEAJZKZLMEBNZLEBNZDOZEDOUELPJUFUHUJQRABCDLEFGHSTUGUIEDUF
      UIEQUEABCEFGUCUAUBUD $.
      $( [20-Dec-2014] $)
  $}

  ${
    $d x G $.  $d x N $.  $d x O $.
    mulgnn0z.b $e |- B = ( Base ` G ) $.
    mulgnn0z.t $e |- T = ( .g ` G ) $.
    mulgnn0z.o $e |- O = ( 0g ` G ) $.
    $( A group multiple of the identity, for nonnegative multiple. $)
    mulgnn0z $p |- ( ( G e. Mnd /\ N e. NN0 ) -> ( N T O ) = O ) $=
      ( vx cn0 wcel cmnd cn cc0 wceq co cfv c1 eqid adantr elnn0 cplusg csn cxp
      wo wa cseq id mndidcl mulgnn syl2anr mndlid mpdan cuz simpr nnuz syl6eleq
      cfz elfznn fvconst2g sylan sylan2 seqid3 eqtrd oveq1 syl sylan9eqr jaodan
      cv mulg0 sylan2b ) DJKCLKZDMKZDNOZUEDEBPZEOZDUAVLVMVPVNVLVMUFZVODCUBQZMEU
      CUDZRUGZQZEVMVMEAKZVOWAOVLVMUHACEFHUIZAVRVTBCDEFVRSZGVTSUJUKVQIVRVSRDEVLE
      EVRPEOZVMVLWBWEWCAVRCEEFWDHULUMTVQDMRUNQVLVMUOUPUQIVIZRDURPKVQWFMKZWFVSQE
      OZWFDUSVQWBWGWHVLWBVMWCTMEWFAUTVAVBVCVDVNVLVONEBPZEDNEBVEVLWBWIEOWCABCEEF
      HGVJVFVGVHVK $.
      $( [13-Dec-2014] $)

    $( A group multiple of the identity, for integer multiple. $)
    mulgz $p |- ( ( G e. Grp /\ N e. ZZ ) -> ( N T O ) = O ) $=
      ( wcel cz wa cn0 cneg co wceq adantl adantr mulgnn0z sylan cfv cgrp wo cr
      elznn0 simprbi cmnd grpmnd cminusg simpll nn0z grpidcl mulgneg syl3anc cc
      eqid zcn ad2antlr negneg syl oveq1d fveq2d grpinvid ad2antrr eqtrd jaodan
      3eqtr3d mpdan ) CUAIZDJIZKZDLIZDMZLIZUBZDEBNZEOZVIVNVHVIDUCIVNDUDUEPVJVKV
      PVMVJCUFIZVKVPVHVQVICUGQZABCDEFGHRSVJVMKZVLMZEBNZVLEBNZCUHTZTZVOEVSVHVLJI
      ZEAIZWAWDOVHVIVMUIVMWEVJVLUJPVJWFVMVHWFVIACEFHUKQQABCWCVLEFGWCUOZULUMVSVT
      DEBVSDUNIZVTDOVIWHVHVMDUPUQDURUSUTVSWDEWCTZEVSWBEWCVJVQVMWBEOVRABCVLEFGHR
      SVAVHWIEOVIVMCWCEHWGVBVCVDVFVEVG $.
      $( [13-Dec-2014] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d x y z M $.  $d x y z N $.  $d x y z P $.
    $d x y z X $.
    mulgnndir.b $e |- B = ( Base ` G ) $.
    mulgnndir.t $e |- T = ( .g ` G ) $.
    mulgnndir.p $e |- P = ( +g ` G ) $.
    $( Sum of group multiples, for positive multiples.  TODO:  This can be
       generalized to a semigroup if/when we introduce them. $)
    mulgnndir $p |- ( ( G e. Mnd /\ ( M e. NN /\ N e. NN /\ X e. B ) ) ->
      ( ( M + N ) T X ) = ( ( M T X ) P ( N T X ) ) ) $=
      ( wcel cn wa caddc co c1 cfv wceq cuz syl2anc vx vy cmnd w3a csn cxp cseq
      vz mndcl 3expb adantlr mndass simpr2 nnuz syl6eleq simpr1 nnz syl eluzadd
      cv cz cc addcom ax-1cn sylancl fveq2d eleq12d mpbird cfz simpr3 fvconst2g
      nncn elfznn syl2an adantr eqeltrd seqsplit nnaddcl mulgnn eqtr4d seqshft2
      eqid syl2anr seqeq1d fveq12d 3eqtr4d oveq12d ) DUCKZELKZFLKZGAKZUDZMZEFNO
      ZBLGUEUFZPUGZQZEWPQZWNBWOEPNOZUGZQZBOWNGCOZEGCOZFGCOZBOWMUAUBUHBAWOPEWNWH
      UAUTZAKZUBUTZAKZMXEXGBOZAKZWLWHXFXHXJABDXEXGHJUIUJUKWHXFXHUHUTZAKUDXIXKBO
      XEXGXKBOBORWLABDXEXGXKHJULUKWMWNWSSQZKFENOZPENOZSQZKZWMFPSQZKEVAKZXPWMFLX
      QWHWIWJWKUMZUNUOZWMWIXRWHWIWJWKUPZEUQURZEPFUSTWMWNXMXLXOWMEVBKZFVBKZWNXMR
      WMWIYCYAEVLURZWMWJYDXSFVLUREFVCTZWMWSXNSWMYCPVBKWSXNRYEVDEPVCVEZVFVGVHWME
      LXQYAUNUOWMXEPWNVIOKZMXEWOQZGAWMWKXELKZYIGRZYHWHWIWJWKVJZXEWNVMLGXEAVKZVN
      WMWKYHYLVOVPVQWMWNLKZWKXBWQRWMWIWJYNYAXSEFVRTYLABWPCDWNGHJIWPWBZVSTWMXCWR
      XDXABWMWIWKXCWRRYAYLABWPCDEGHJIYOVSTWMFWPQZXMBWOXNUGZQXDXAWMBUAWOWOEPFXTY
      BWMXEPFVIOKZMZYIGXEENOZWOQZWMWKYJYKYRYLXEFVMZYMVNYSWKYTLKZUUAGRWMWKYRYLVO
      YRYJWIUUCWMUUBYAXEEVRWCLGYTAVKTVTWAWMWJWKXDYPRXSYLABWPCDFGHJIYOVSTWMWNXMW
      TYQWMWSXNBWOYGWDYFWEWFWGWF $.
      $( [11-Dec-2014] $)

    $( Sum of group multiples, generalized to ` NN0 ` . $)
    mulgnn0dir $p |- ( ( G e. Mnd /\ ( M e. NN0 /\ N e. NN0 /\ X e. B ) ) ->
      ( ( M + N ) T X ) = ( ( M T X ) P ( N T X ) ) ) $=
      ( wcel cn0 wa cc0 wceq caddc co simpr oveq1d eqtrd w3a cn wo simpr1 elnn0
      sylib simpr2 adantr simplll simplr simpr3 ad2antrr mulgnndir syl13anc c0g
      cmnd cfv simpll simplr3 mulgnn0cl syl3anc mndrid syl2anc mulg0 syl oveq2d
      eqid cc nn0cn addid1 3eqtr4rd adantlr jaodan mpdan simplr2 mndlid addid2
      3syl ) DUPKZELKZFLKZGAKZUAZMZEUBKZENOZUCZEFPQZGCQZEGCQZFGCQZBQZOZWDVTWGVS
      VTWAWBUDZEUEUFWDWEWMWFWDWEMZFUBKZFNOZUCZWMWDWRWEWDWAWRVSVTWAWBUGFUEUFUHWO
      WPWMWQWOWPMVSWEWPWBWMVSWCWEWPUIWDWEWPUJWOWPRWDWBWEWPVSVTWAWBUKULABCDEFGHI
      JUMUNWDWQWMWEWDWQMZWJDUOUQZBQZWJWLWIWSVSWJAKZXAWJOVSWCWQURZWSVSVTWBXBXCWD
      VTWQWNUHZVTWAWBVSWQUSZACDEGHIUTVAABDWTWJHJWTVGZVBVCWSWKWTWJBWSWKNGCQZWTWS
      FNGCWDWQRZSWSWBXGWTOZXEACDWTGHXFIVDZVETVFWSWHEGCWSWHENPQZEWSFNEPXHVFWSVTE
      VHKXKEOXDEVIEVJVRTSVKVLVMVNWDWFMZWTWKBQZWKWLWIXLVSWKAKZXMWKOVSWCWFURZXLVS
      WAWBXNXOVTWAWBVSWFVOZVTWAWBVSWFUSZACDFGHIUTVAABDWTWKHJXFVPVCXLWJWTWKBXLWJ
      XGWTXLENGCWDWFRZSXLWBXIXQXJVETSXLWHFGCXLWHNFPQZFXLENFPXRSXLWAFVHKXSFOXPFV
      IFVQVRTSVKVMVN $.
      $( [11-Dec-2014] $)

    $( Lemma for ~ mulgdir . $)
    mulgdirlem $p |- ( ( G e. Grp /\ ( M e. ZZ /\ N e. ZZ /\ X e. B ) /\
      ( M + N ) e. NN0 ) -> ( ( M + N ) T X ) = ( ( M T X ) P ( N T X ) ) ) $=
      ( wcel cz co cn0 wceq syl wa adantr syl3anc syl2anc cgrp w3a caddc simp21
      cneg wo cr elznn0 simprbi simp22 cmnd simpl1 grpmnd simprl simprr simpl23
      mulgnn0dir syl13anc anassrs simpl3 nn0z mulgcl ad2antll grpass cmin addcl
      zcn negsub pncan eqtrd oveq1d eqtr3d c0g cfv cminusg eqid mulgneg grplinv
      cc oveq2d grprid 3eqtr3rd jaodan mpdan wi znegcl simp3 grplid simpr negcl
      grprinv addcom pncan2 3eqtrd 3eqtr3d idi ) DUAKZELKZFLKZGAKZUBZEFUCMZNKZU
      BZENKZEUEZNKZUFZXBGCMZEGCMZFGCMZBMZOZXDWRXHWQWRWSWTXCUDZWREUGKXHEUHUIPXDX
      EXMXGXDXEQZFNKZFUEZNKZUFZXMXDXSXEXDWSXSWQWRWSWTXCUJZWSFUGKXSFUHUIPRXOXPXM
      XRXDXEXPXMXDXEXPQZQZDUKKZXEXPWTXMYBWQYCWQXAXCYAULDUMZPXDXEXPUNXDXEXPUOWRW
      SWTWQXCYAUPABCDEFGHIJUQURUSXDXEXRXMXDXEXRQZQZXIXQGCMZBMZXKBMZXIYGXKBMZBMZ
      XLXIYFWQXIAKZYGAKZXKAKZYIYKOWQXAXCYEULZYFWQXBLKZWTYLYOYFXCYPWQXAXCYEUTZXB
      VAZPWRWSWTWQXCYEUPZACDXBGHIVBZSZYFWQXQLKZWTYMYOXRUUBXDXEXQVAVCYSACDXQGHIV
      BSYFWQWSWTYNYOXDWSYEXTRZYSACDFGHIVBSZABDXIYGXKHJVDURYFYHXJXKBYFXBXQUCMZGC
      MZYHXJYFYCXCXRWTUUFYHOYFWQYCYOYDPYQXDXEXRUOYSABCDXBXQGHIJUQURYFUUEEGCYFUU
      EXBFVEMZEYFXBVSKZFVSKZUUEUUGOXDUUHYEXDEVSKZUUIUUHXDWRUUJXNEVGPZXDWSUUIXTF
      VGPZEFVFTZRXDUUIYEUULRZXBFVHTYFUUJUUIUUGEOXDUUJYEUUKRUUNEFVITVJVKVLVKYFYK
      XIDVMVNZBMZXIYFYJUUOXIBYFYJXKDVOVNZVNZXKBMZUUOYFYGUURXKBYFWQWSWTYGUUROYOU
      UCYSACDUUQFGHIUUQVPZVQSVKYFWQYNUUSUUOOYOUUDABDUUQUUOXKHJUUOVPZUUTVRTVJVTY
      FWQYLUUPXIOYOUUAABDUUOXIHJUVAWATVJWBUSWCWDXDXGQZXMWEUVBXJXFGCMZBMZXIBMZXJ
      UVCXIBMZBMZXIXLUVBWQXJAKZUVCAKZYLUVEUVGOWQXAXCXGULZUVBWQWRWTUVHUVJXDWRXGX
      NRZWRWSWTWQXCXGUPZACDEGHIVBSZUVBWQXFLKZWTUVIUVJUVBWRUVNUVKEWFPUVLACDXFGHI
      VBSUVBWQYPWTYLUVJXDYPXGXDXCYPWQXAXCWGYRPRUVLYTSZABDXJUVCXIHJVDURUVBUVEUUO
      XIBMZXIUVBUVDUUOXIBUVBUVDXJXJUUQVNZBMZUUOUVBUVCUVQXJBUVBWQWRWTUVCUVQOUVJU
      VKUVLACDUUQEGHIUUTVQSVTUVBWQUVHUVRUUOOUVJUVMABDUUQUUOXJHJUVAUUTWKTVJVKUVB
      WQYLUVPXIOUVJUVOABDUUOXIHJUVAWHTVJUVBUVFXKXJBUVBXFXBUCMZGCMZUVFXKUVBYCXGX
      CWTUVTUVFOUVBWQYCUVJYDPXDXGWIWQXAXCXGUTUVLABCDXFXBGHIJUQURUVBUVSFGCUVBUVS
      XBXFUCMZXBEVEMZFUVBXFVSKZUUHUVSUWAOUVBUUJUWCXDUUJXGUUKRZEWJPXDUUHXGUUMRZX
      FXBWLTUVBUUHUUJUWAUWBOUWEUWDXBEVHTUVBUUJUUIUWBFOUWDXDUUIXGUULREFWMTWNVKVL
      VTWOWPWCWD $.
      $( [13-Dec-2014] $)

    $( Sum of group multiples, generalized to ` ZZ ` . $)
    mulgdir $p |- ( ( G e. Grp /\ ( M e. ZZ /\ N e. ZZ /\ X e. B ) ) ->
      ( ( M + N ) T X ) = ( ( M T X ) P ( N T X ) ) ) $=
      ( wcel cz co cn0 wceq syl2anc syl cfv cc syl3anc cgrp w3a wa caddc simpr1
      wo simpr2 zaddcl cr elznn0 simprbi mulgdirlem 3expa cminusg simpll adantr
      cneg znegcl simplr3 zcn negdi negcl addcom eqtrd simpr eqeltrrd syl131anc
      oveq1d eqid mulgneg eqtr3d oveq12d mulgcl grpinvadd eqtr4d 3eqtr3d fveq2d
      grpinvinv grpcl jaodan mpdan ) DUAKZELKZFLKZGAKZUBZUCZEFUDMZNKZWHUQZNKZUF
      ZWHGCMZEGCMZFGCMZBMZOZWGWHLKZWLWGWCWDWRWBWCWDWEUEZWBWCWDWEUGZEFUHPZWRWHUI
      KWLWHUJUKQWGWIWQWKWBWFWIWQABCDEFGHIJULUMWGWKUCZWMDUNRZRZXCRZWPXCRZXCRZWMW
      PXBXDXFXCXBFUQZEUQZUDMZGCMZXHGCMZXIGCMZBMZXDXFXBWBXHLKZXILKZWEXJNKXKXNOWB
      WFWKUOZXBWDXOWGWDWKWTUPZFURQXBWCXPWGWCWKWSUPZEURQWCWDWEWBWKUSZXBWJXJNXBWJ
      XIXHUDMZXJXBESKZFSKZWJYAOXBWCYBXSEUTQZXBWDYCXRFUTQZEFVAPXBXISKZXHSKZYAXJO
      XBYBYFYDEVBQXBYCYGYEFVBQXIXHVCPVDZWGWKVEVFABCDXHXIGHIJULVGXBWJGCMZXKXDXBW
      JXJGCYHVHXBWBWRWEYIXDOXQWGWRWKXAUPZXTACDXCWHGHIXCVIZVJTVKXBXNWOXCRZWNXCRZ
      BMZXFXBXLYLXMYMBXBWBWDWEXLYLOXQXRXTACDXCFGHIYKVJTXBWBWCWEXMYMOXQXSXTACDXC
      EGHIYKVJTVLXBWBWNAKZWOAKZXFYNOXQXBWBWCWEYOXQXSXTACDEGHIVMTZXBWBWDWEYPXQXR
      XTACDFGHIVMTZABDXCWNWOHJYKVNTVOVPVQXBWBWMAKZXEWMOXQXBWBWRWEYSXQYJXTACDWHG
      HIVMTADXCWMHYKVRPXBWBWPAKZXGWPOXQXBWBYOYPYTXQYQYRABDWNWOHJVSTADXCWPHYKVRP
      VPVTWA $.
      $( [13-Dec-2014] $)

    $( Group multiple (exponentiation) operation at a successor, extended to
       ` ZZ ` . $)
    mulgp1 $p |- ( ( G e. Grp /\ N e. ZZ /\ X e. B ) ->
      ( ( N + 1 ) T X ) = ( ( N T X ) P X ) ) $=
      ( cgrp wcel cz w3a c1 caddc co wceq 1z mulgdir mp3anr2 3impb mulg1 oveq2d
      3ad2ant3 eqtrd ) DJKZELKZFAKZMZENOPFCPZEFCPZNFCPZBPZUKFBPUFUGUHUJUMQZUFUG
      NLKUHUNRABCDENFGHISTUAUIULFUKBUHUFULFQUGACDFGHUBUDUCUE $.
      $( [11-Dec-2014] $)
  $}

  ${
    $d n x B $.  $d n x G $.  $d n x I $.  $d x N $.  $d n x T $.  $d n x X $.
    mulgneg2.b $e |- B = ( Base ` G ) $.
    mulgneg2.m $e |- T = ( .g ` G ) $.
    mulgneg2.i $e |- I = ( invg ` G ) $.
    $( Group multiple (exponentiation) operation at a negative integer. $)
    mulgneg2 $p |- ( ( G e. Grp /\ N e. ZZ /\ X e. B ) ->
      ( -u N T X ) = ( N T ( I ` X ) ) ) $=
      ( wcel cneg co cfv wceq cc0 c1 negeq oveq1d oveq1 eqeq12d vx vn cgrp neg0
      cz cv caddc wa syl6eq c0g eqid mulg0 adantl grpinvcl syl eqtr4d wi cplusg
      cn0 cc nn0cn ax-1cn negdi sylancl simpll nn0negz 1z znegcl simplr mulgdir
      syl13anc mulgm1 adantr oveq2d 3eqtrd cmnd grpmnd ad2antrr simpr mulgnn0p1
      a1i syl3anc syl5ibr ex cn fveq2 nnnegz mulgneg id mulgnegnn syl2anr zindd
      3impia 3com23 ) CUCJZFAJZEUEJZEKZFBLZEFDMZBLZNZWOWPWQXBUAUFZKZFBLZXCWTBLZ
      NOFBLZOWTBLZNUBUFZKZFBLZXIWTBLZNZXJKZFBLZXJWTBLZNZXIPUGLZKZFBLZXRWTBLZNZX
      BWOWPUHZUAUBEXCONZXEXGXFXHYDXDOFBYDXDOKOXCOQUDUIRXCOWTBSTXCXINZXEXKXFXLYE
      XDXJFBXCXIQRXCXIWTBSTXCXRNZXEXTXFYAYFXDXSFBXCXRQRXCXRWTBSTXCXJNZXEXOXFXPY
      GXDXNFBXCXJQRXCXJWTBSTXCENZXEWSXFXAYHXDWRFBXCEQRXCEWTBSTYCXGCUJMZXHWPXGYI
      NWOABCYIFGYIUKZHULUMYCWTAJZXHYINACDFGIUNZABCYIWTGYJHULUOUPYCXIUSJZXMYBUQX
      MYBYCYMUHZXKWTCURMZLZXLWTYOLZNXKXLWTYOSYNXTYPYAYQYNXTXJPKZUGLZFBLZXKYRFBL
      ZYOLZYPYNXSYSFBYNXIUTJZPUTJXSYSNYMUUCYCXIVAUMVBXIPVCVDRYNWOXJUEJZYRUEJZWP
      YTUUBNWOWPYMVEYMUUDYCXIVFUMYNPUEJZUUEUUFYNVGWAPVHUOWOWPYMVIAYOBCXJYRFGHYO
      UKZVJVKYNUUAWTXKYOYCUUAWTNYMABCDFGHIVLVMVNVOYNCVPJZYMYKYAYQNWOUUHWPYMCVQV
      RYCYMVSYCYKYMYLVMAYOBCXIWTGHUUGVTWBTWCWDYCXIWEJZXMXQUQXMXQYCUUIUHZXKDMZXL
      DMZNXKXLDWFUUJXOUUKXPUULUUJWOUUDWPXOUUKNWOWPUUIVEUUIUUDYCXIWGUMWOWPUUIVIA
      BCDXJFGHIWHWBUUIUUIYKXPUULNYCUUIWIYLABCDXIWTGHIWJWKTWCWDWLWMWN $.
      $( [13-Dec-2014] $)
  $}

  ${
    $d m n B $.  $d m n G $.  $d n M $.  $d m n N $.  $d m n T $.  $d m n X $.
    mulgass.b $e |- B = ( Base ` G ) $.
    mulgass.t $e |- T = ( .g ` G ) $.
    $( Product of group multiples, for positive multiples.  TODO:  This can be
       generalized to a semigroup if/when we introduce them. $)
    mulgnnass $p |- ( ( G e. Mnd /\ ( M e. NN /\ N e. NN /\ X e. B ) ) ->
      ( ( M x. N ) T X ) = ( M T ( N T X ) ) ) $=
      ( wcel cn cmul co wceq wi c1 caddc oveq1 oveq1d eqeq12d imbi2d vn vm cmnd
      w3a cv cc nncn mulid2 syl 3ad2ant1 mulgnncl 3coml mulg1 eqtr4d cplusg cfv
      wa adantr ax-1cn simpr1 adddir syl3anc adantl oveq2d eqtrd simpr3 nnmulcl
      3ad2antr1 simpr2 eqid mulgnndir syl13anc mulgnnp1 sylan2 syl5ibr ex nnind
      a1i a2d 3expd com4r 3imp2 ) CUCIZDJIZEJIZFAIZDEKLZFBLZDEFBLZBLZMZWDWEWFWC
      WKWDWEWFWCWKWEWFWCUDZUAUEZEKLZFBLZWMWIBLZMZNWLOEKLZFBLZOWIBLZMZNWLUBUEZEK
      LZFBLZXBWIBLZMZNWLXBOPLZEKLZFBLZXGWIBLZMZNWLWKNUAUBDWMOMZWQXAWLXLWOWSWPWT
      XLWNWRFBWMOEKQRWMOWIBQSTWMXBMZWQXFWLXMWOXDWPXEXMWNXCFBWMXBEKQRWMXBWIBQSTW
      MXGMZWQXKWLXNWOXIWPXJXNWNXHFBWMXGEKQRWMXGWIBQSTWMDMZWQWKWLXOWOWHWPWJXOWNW
      GFBWMDEKQRWMDWIBQSTWLWSWIWTWLWREFBWEWFWREMZWCWEEUFIZXPEUGZEUHUIUJZRWLWIAI
      ZWTWIMWCWEWFXTABCEFGHUKULZABCWIGHUMUIUNXBJIZWLXFXKYBWLXFXKNXFXKYBWLUQZXDW
      ICUOUPZLZXEWIYDLZMXDXEWIYDQYCXIYEXJYFYCXIXCEPLZFBLZYEYCXHYGFBYCXHXCWRPLZY
      GYCXBUFIZOUFIZXQXHYIMYBYJWLXBUGURYKYCUSVRYCWEXQYBWEWFWCUTZXRUIXBOEVAVBYCW
      REXCPWLXPYBXSVCVDVERYCWCXCJIZWEWFYHYEMYBWEWFWCVFYBWFWEYMWCXBEVGVHYLYBWEWF
      WCVIAYDBCXCEFGHYDVJZVKVLVEWLYBXTXJYFMYAAYDBCXBWIGHYNVMVNSVOVPVSVQVTWAWB
      $.
      $( [13-Dec-2014] $)

    $( Product of group multiples, generalized to ` NN0 ` . $)
    mulgnn0ass $p |- ( ( G e. Mnd /\ ( M e. NN0 /\ N e. NN0 /\ X e. B ) ) ->
      ( ( M x. N ) T X ) = ( M T ( N T X ) ) ) $=
      ( wcel cn0 wa cn cc0 wceq wo cmul co adantr oveq1d oveq1 w3a simpr1 elnn0
      cmnd sylib simpr2 simpl simprl simprr simpr3 mulgnnass syl13anc expr eqid
      c0g cfv mulg0 syl cc nn0cn mul01 3syl oveq2d mulgnn0z syl2anc eqtrd oveq2
      3eqtr4d eqeq12d syl5ibrcom jaod mpd ex mul02 mulgnn0cl syl3anc ) CUDIZDJI
      ZEJIZFAIZUAZKZDLIZDMNZOZDEPQZFBQZDEFBQZBQZNZWBVRWEVQVRVSVTUBZDUCUEWBWCWJW
      DWBWCWJWBWCKZELIZEMNZOZWJWBWOWCWBVSWOVQVRVSVTUFZEUCUERWLWMWJWNWBWCWMWJWBW
      CWMKZKVQWCWMVTWJWBVQWQVQWAUGZRWBWCWMUHWBWCWMUIWBVTWQVQVRVSVTUJZRABCDEFGHU
      KULUMWLWJWNDMPQZFBQZDMFBQZBQZNZWBXDWCWBXBCUOUPZXAXCWBVTXBXENWSABCXEFGXEUN
      ZHUQURZWBWTMFBWBVRDUSIWTMNWKDUTDVAVBSWBXCDXEBQZXEWBXBXEDBXGVCWBVQVRXHXENW
      RWKABCDXEGHXFVDVEVFVHRWNWGXAWIXCWNWFWTFBEMDPVGSWNWHXBDBEMFBTVCVIVJVKVLVMW
      BWJWDMEPQZFBQZMWHBQZNWBXBXEXJXKXGWBXIMFBWBVSEUSIXIMNWPEUTEVNVBSWBWHAIZXKX
      ENWBVQVSVTXLWRWPWSABCEFGHVOVPABCXEWHGXFHUQURVHWDWGXJWIXKWDWFXIFBDMEPTSDMW
      HBTVIVJVKVL $.
      $( [13-Dec-2014] $)

    $( Product of group multiples, generalized to ` ZZ ` . $)
    mulgass $p |- ( ( G e. Grp /\ ( M e. ZZ /\ N e. ZZ /\ X e. B ) ) ->
      ( ( M x. N ) T X ) = ( M T ( N T X ) ) ) $=
      ( wcel cz wa cn0 cneg co wceq syl syl2anc adantr cfv syl3anc cgrp wo cmul
      w3a simpr1 elznn0 simprbi simpr2 cmnd simpll grpmnd simprl simprr simplr3
      cr mulgnn0ass syl13anc ex cc zcn mulneg1 oveq1d simpl simpr3 eqtr3d fveq2
      cminusg zmulcl eqid mulgneg fveq2d mulgcl grpinvinv eqeq12d syl5ib syldan
      eqtrd mulneg2 oveq2d mulgneg2 eqtr4d 3eqtr3d mul2neg nn0z ad2antll negneg
      imp 3syl ccased mp2and ) CUAIZDJIZEJIZFAIZUDZKZDLIZDMZLIZUBZELIZEMZLIZUBZ
      DEUCNZFBNZDEFBNZBNZOZWPWLWTWKWLWMWNUEZWLDUOIWTDUFUGPWPWMXDWKWLWMWNUHZWMEU
      OIXDEUFUGPWPWQXAWSXCXIWPWQXAKZXIWPXLKZCUIIZWQXAWNXIXMWKXNWKWOXLUJCUKZPWPW
      QXAULWPWQXAUMWLWMWNWKXLUNABCDEFGHUPUQURWPWSXAKZXIWPXPXEMZFBNZWRXGBNZOZXIW
      PXPKZWREUCNZFBNZXRXSYAYBXQFBWPYBXQOZXPWPDUSIZEUSIZYDWPWLYEXJDUTPZWPWMYFXK
      EUTZPZDEVAQRVBYAXNWSXAWNYCXSOYAWKXNWPWKXPWKWOVCZRXOPWPWSXAULWPWSXAUMWPWNX
      PWKWLWMWNVDZRABCWREFGHUPUQVEWPXTXIXTXRCVGSZSZXSYLSZOWPXIXRXSYLVFWPYMXFYNX
      HWPYMXFYLSZYLSZXFWPXRYOYLWPWKXEJIZWNXRYOOYJWPWLWMYQXJXKDEVHQZYKABCYLXEFGH
      YLVIZVJTVKWPWKXFAIZYPXFOYJWPWKYQWNYTYJYRYKABCXEFGHVLTACYLXFGYSVMQVQWPYNXH
      YLSZYLSZXHWPXSUUAYLWPWKWLXGAIZXSUUAOYJXJWPWKWMWNUUCYJXKYKABCEFGHVLTZABCYL
      DXGGHYSVJTVKWPWKXHAIZUUBXHOYJWPWKWLUUCUUEYJXJUUDABCDXGGHVLTACYLXHGYSVMQVQ
      VNVOWGZVPURWPWQXCKZXIWPUUGXTXIWPUUGKZDXBUCNZFBNZDXBFBNZBNZXRXSUUHXNWQXCWN
      UUJUULOUUHWKXNWPWKUUGYJRXOPWPWQXCULWPWQXCUMWPWNUUGYKRABCDXBFGHUPUQUUHUUIX
      QFBWPUUIXQOZUUGWPYEYFUUMYGYIDEVRQRVBWPUULXSOUUGWPUULDXGYLSZBNZXSWPUUKUUND
      BWPWKWMWNUUKUUNOYJXKYKABCYLEFGHYSVJTVSWPWKWLUUCXSUUOOYJXJUUDABCYLDXGGHYSV
      TTWARWBUUFVPURWPWSXCKZXIWPUUPKZWRXBUCNZFBNZWRUUKBNZXFXHUUQXNWSXCWNUUSUUTO
      UUQWKXNWPWKUUPYJRZXOPWPWSXCULWPWSXCUMWPWNUUPYKRZABCWRXBFGHUPUQWPUUSXFOUUP
      WPUURXEFBWPYEYFUURXEOYGYIDEWCQVBRUUQUUTDUUKYLSZBNZXHUUQWKWLUUKAIZUUTUVDOU
      VAWPWLUUPXJRUUQWKXBJIZWNUVEUVAXCUVFWPWSXBWDWEZUVBABCXBFGHVLTABCYLDUUKGHYS
      VTTUUQUVCXGDBUUQXBMZFBNZUVCXGUUQWKUVFWNUVIUVCOUVAUVGUVBABCYLXBFGHYSVJTUUQ
      UVHEFBWPUVHEOZUUPWPWMYFUVJXKYHEWFWHRVBVEVSVQWBURWIWJ $.
      $( [13-Dec-2014] $)
  $}

  ${
    mulgsubdir.b $e |- B = ( Base ` G ) $.
    mulgsubdir.t $e |- T = ( .g ` G ) $.
    mulgsubdir.d $e |- D = ( -g ` G ) $.
    $( Subtraction of a group element from itself. $)
    mulgsubdir $p |- ( ( G e. Grp /\ ( M e. ZZ /\ N e. ZZ /\ X e. B ) ) ->
      ( ( M - N ) T X ) = ( ( M T X ) D ( N T X ) ) ) $=
      ( wcel cz co cfv wceq syl eqid cc zcn syl2anc cgrp w3a wa cneg caddc cmin
      cplusg simpr1 simpr2 znegcl simpr3 mulgdir syl13anc negsub oveq1d cminusg
      simpl mulgneg 3adant3r1 oveq2d mulgcl 3adant3r2 grpsubval eqtr4d 3eqtr3d
      ) DUAKZELKZFLKZGAKZUBZUCZEFUDZUEMZGCMZEGCMZVLGCMZDUGNZMZEFUFMZGCMVOFGCMZB
      MZVKVFVGVLLKZVIVNVROVFVJUQVFVGVHVIUHZVKVHWBVFVGVHVIUIZFUJPVFVGVHVIUKAVQCD
      EVLGHIVQQZULUMVKVMVSGCVKERKZFRKZVMVSOVKVGWFWCESPVKVHWGWDFSPEFUNTUOVKVRVOV
      TDUPNZNZVQMZWAVKVPWIVOVQVFVHVIVPWIOVGACDWHFGHIWHQZURUSUTVKVOAKZVTAKZWAWJO
      VFVGVIWLVHACDEGHIVAVBVFVHVIWMVGACDFGHIVAUSAVQDWHBVOVTHWEWKJVCTVDVE $.
      $( [13-Dec-2014] $)
  $}

