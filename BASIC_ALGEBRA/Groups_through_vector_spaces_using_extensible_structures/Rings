$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Ring $.
  $c 1r $.

  $( Extend class notation with class of all rings. $)
  crg $a class Ring $.

  $( Extend class notation with ring unit. $)
  cur $a class 1r $.

  ${
    $d f p r t u x y z $.
    $( Define class of all rings (with unity). $)
    df-ring $a |- Ring = { f e. Grp |
        E. r E. p E. t ( ( r = ( Base ` f ) /\ p = ( +g ` f )
           /\ t = ( .r ` f ) )
       /\ A. x e. r A. y e. r A. z e. r
           ( ( x t y ) e. r /\ ( ( ( x t y ) t z ) = ( x t ( y t z ) )
            /\ ( x t ( y p z ) ) = ( ( x t y ) p ( x t z ) )
            /\ ( ( x p y ) t z ) = ( ( x t z ) p ( y t z ) ) ) )
        /\ E. u e. r A. x e. r ( ( u t x ) = x /\ ( x t u ) = x ) ) } $.

    $( Define ring unity. $)
    df-ur $a |- 1r = ( 0g o. mulGrp ) $.
  $}

  ${
    $d u r x B $.  $d u r x R $.  $d r T $.
    rngidval.g $e |- G = ( mulGrp ` R ) $.
    rngidval.u $e |- U = ( 1r ` R ) $.
    $( The value of the unity element of a ring.  (Revised by Mario Carneiro,
       27-Dec-2014.) $)
    rngidval $p |- ( R e. V -> U = ( 0g ` G ) ) $=
      ( vr wcel c0g cmgp ccom cfv cvv wfn wceq cv cnx cplusg csts cur cmulr cop
      df-mgp fnmpt ovex a1i mprg elex fvco2 sylancr df-ur fveq1i fveq2i 3eqtr4g
      co eqtri ) ADHZAIJKZLZAJLZILZBCILUQJMNZAMHUSVAOGPZQRLVCUALUBZSUOZMHZVBGMG
      MVEJMGUCUDVFVCMHVCVDSUEUFUGADUHMIJAUIUJBATLUSFATURUKULUPCUTIEUMUN $.
      $( [27-Dec-2014] $) $( [27-Aug-2011] $)
  $}

  ${
    $d b r p t u x y z B $.  $d b r p t u x y z P $.  $d b r p t R $.
    $d b p r t u x y z T $.
    isrng.b $e |- B = ( Base ` R ) $.
    isrng.p $e |- P = ( +g ` R ) $.
    isrng.t $e |- T = ( .r ` R ) $.
    $( The predicate "is a (unital) ring."  Definition of ring with unit in
       [Schechter] p. 187. $)
    isrng $p |- ( R e. Ring <-> ( R e. Grp
             /\ A. x e. B A. y e. B A. z e. B ( ( x T y ) e. B
               /\ ( ( ( x T y ) T z ) = ( x T ( y T z ) )
            /\ ( x T ( y P z ) ) = ( ( x T y ) P ( x T z ) )
            /\ ( ( x P y ) T z ) = ( ( x T z ) P ( y T z ) ) ) )
           /\ E. u e. B A. x e. B ( ( u T x ) = x /\ ( x T u ) = x ) ) ) $=
      ( vb vp cv wceq w3a co wa wral oveq vr crg wcel cbs cfv cplusg cmulr wrex
      vt cgrp crab df-ring eleq2i fveq2 syl6eqr eqeq2d 3anbi123d 3anbi1d 3anass
      wex syl6bb 3exbidv elrab eqeltri eleq2 anbi1d raleqbi1dv raleq rexeqbi1dv
      cvv anbi12d oveq2d eqeq12d oveq1d 3anbi23d anbi2d ralbidv 2ralbidv eleq1d
      fvex eqtrd oveq12d eqeq1d rexralbidv ceqsex3v anbi2i bitr4i 3bitri ) GUBU
      CGLNZUANZUDUEZOZMNZWJUFUEZOZUINZWJUGUEZOZPZANZBNZWPQZWIUCZXBCNZWPQZWTXAXD
      WPQZWPQZOZWTXAXDWMQZWPQZXBWTXDWPQZWMQZOZWTXAWMQZXDWPQZXKXFWMQZOZPZRZCWISZ
      BWISZAWISZDNZWTWPQZWTOZWTYCWPQZWTOZRZAWISZDWIUHZPZUIUTMUTLUTZUAUJUKZUCGUJ
      UCZWIEOZWMFOZWPHOZPZYBYJRZRZUIUTMUTLUTZRZYNWTXAHQZEUCZUUCXDHQZWTXAXDHQZHQ
      ZOZWTXAXDFQZHQZUUCWTXDHQZFQZOZWTXAFQZXDHQZUUKUUFFQZOZPZRZCESZBESAESZYCWTH
      QZWTOZWTYCHQZWTOZRZAESDEUHZPZUBYMGABCDUIUALMULUMYLUUAUAGUJWJGOZYKYTLMUIUV
      IYKYRYBYJPYTUVIWSYRYBYJUVIWLYOWOYPWRYQUVIWKEWIUVIWKGUDUEZEWJGUDUNIUOUPUVI
      WNFWMUVIWNGUFUEZFWJGUFUNJUOUPUVIWQHWPUVIWQGUGUEZHWJGUGUNKUOUPUQURYRYBYJUS
      VAVBVCUUBYNUVAUVGRZRUVHUUAUVMYNYSXBEUCZXRRZCESZBESZAESZYHAESZDEUHZRUVNXHW
      TUUIWPQZXBXKFQZOZUUNXDWPQZXKXFFQZOZPZRZCESZBESAESZUVTRUVMLMUIEFHEUVJVJIGU
      DVTVDFUVKVJJGUFVTVDHUVLVJKGUGVTVDYOYBUVRYJUVTYAUVQAWIEXTUVPBWIEXSUVOCWIEY
      OXCUVNXRWIEXBVEVFVGVGVGYIUVSDWIEYHAWIEVHVIVKYPUVRUWJUVTYPUVPUWIABEEYPUVOU
      WHCEYPXRUWGUVNYPXMUWCXQUWFXHYPXJUWAXLUWBYPXIUUIWTWPXAXDWMFTVLXBXKWMFTVMYP
      XOUWDXPUWEYPXNUUNXDWPWTXAWMFTVNXKXFWMFTVMVOVPVQVRVFYQUWJUVAUVTUVGYQUWIUUT
      ABEEYQUWHUUSCEYQUVNUUDUWGUURYQXBUUCEWTXAWPHTZVSYQXHUUHUWCUUMUWFUUQYQXEUUE
      XGUUGYQXEXBXDHQUUEXBXDWPHTYQXBUUCXDHUWKVNWAYQXGWTXFHQUUGWTXFWPHTYQXFUUFWT
      HXAXDWPHTZVLWAVMYQUWAUUJUWBUULWTUUIWPHTYQXBUUCXKUUKFUWKWTXDWPHTZWBVMYQUWD
      UUOUWEUUPUUNXDWPHTYQXKUUKXFUUFFUWMUWLWBVMUQVKVQVRYQYHUVFDAEEYQYEUVCYGUVEY
      QYDUVBWTYCWTWPHTWCYQYFUVDWTWTYCWPHTWCVKWDVKWEWFYNUVAUVGUSWGWH $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d b p r t u x y z B $.  $d b p r t u x y z P $.  $d b n p r t R $.
    $d b p r t u x y z T $.  $d n u x y z $.
    rngi.b $e |- B = ( Base ` R ) $.
    rngi.p $e |- P = ( +g ` R ) $.
    rngi.t $e |- T = ( .r ` R ) $.
    $d x y z X $.  $d y z Y $.  $d z Z $.
    $( Properties of a unital ring. $)
    rngi $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) )
           -> ( ( X T Y ) e. B
                /\ ( ( ( X T Y ) T Z ) = ( X T ( Y T Z ) )
              /\ ( X T ( Y P Z ) ) = ( ( X T Y ) P ( X T Z ) )
                /\ ( ( X P Y ) T Z ) = ( ( X T Z ) P ( Y T Z ) ) ) ) ) $=
      ( vx wcel co wceq w3a oveq1 oveq1d eqeq12d oveq2 oveq2d vy vz vu crg wral
      cv cgrp wrex isrng simp2bi eleq1d oveq12d 3anbi123d anbi12d rcla43v mpan9
      wa anbi2d ) CUDLZKUFZUAUFZDMZALZVBUBUFZDMZUTVAVDDMZDMZNZUTVAVDBMZDMZVBUTV
      DDMZBMZNZUTVABMZVDDMZVKVFBMZNZOZUQZUBAUEUAAUEKAUEZEALFALGALOEFDMZALZWAGDM
      ZEFGDMZDMZNZEFGBMZDMZWAEGDMZBMZNZEFBMZGDMZWIWDBMZNZOZUQZUSCUGLVTUCUFZUTDM
      UTNUTWRDMUTNUQKAUEUCAUHKUAUBUCABCDHIJUIUJVSWQEVADMZALZWSVDDMZEVFDMZNZEVID
      MZWSEVDDMZBMZNZEVABMZVDDMZXEVFBMZNZOZUQWBWAVDDMZEFVDDMZDMZNZEFVDBMZDMZWAX
      EBMZNZWLVDDMZXEXNBMZNZOZUQKUAUBEFGAAAUTENZVCWTVRXLYEVBWSAUTEVADPZUKYEVHXC
      VMXGVQXKYEVEXAVGXBYEVBWSVDDYFQUTEVFDPRYEVJXDVLXFUTEVIDPYEVBWSVKXEBYFUTEVD
      DPZULRYEVOXIVPXJYEVNXHVDDUTEVABPQYEVKXEVFBYGQRUMUNVAFNZWTWBXLYDYHWSWAAVAF
      EDSZUKYHXCXPXGXTXKYCYHXAXMXBXOYHWSWAVDDYIQYHVFXNEDVAFVDDPZTRYHXDXRXFXSYHV
      IXQEDVAFVDBPTYHWSWAXEBYIQRYHXIYAXJYBYHXHWLVDDVAFEBSQYHVFXNXEBYJTRUMUNVDGN
      ZYDWPWBYKXPWFXTWKYCWOYKXMWCXOWEVDGWADSYKXNWDEDVDGFDSZTRYKXRWHXSWJYKXQWGED
      VDGFBSTYKXEWIWABVDGEDSZTRYKYAWMYBWNVDGWLDSYKXEWIXNWDBYMYLULRUMURUOUP $.
      $( [26-Aug-2011] $)
  $}

  ${
    rngcl.b $e |- B = ( Base ` R ) $.
    rngcl.t $e |- T = ( .r ` R ) $.
    $( Closure of the multiplication operation of a ring. $)
    rngcl $p |- ( ( R e. Ring /\ X e. B /\ Y e. B ) -> ( X T Y ) e. B ) $=
      ( crg wcel co wa w3a 3anass anidm anbi2i bitri wceq cplusg cfv eqid 3impb
      rngi simpld sylan2br ) BHIZDAIZEAIZDECJZAIZUFUGKZUEUFUGUGLZUIUKUFUGUGKZKU
      JUFUGUGMULUGUFUGNOPUEUKKUIUHECJDEECJZCJQDEEBRSZJCJUHUHUNJQDEUNJECJUHUMUNJ
      QLAUNBCDEEFUNTGUBUCUDUA $.
      $( [26-Aug-2011] $)

    $( Associative law for the multiplication operation of a ring. $)
    rngass $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                      ( ( X T Y ) T Z ) = ( X T ( Y T Z ) ) ) $=
      ( crg wcel w3a wa co wceq cplusg cfv eqid rngi simpr1 syl ) BIJDAJEAJFAJK
      LDECMZAJZUAFCMDEFCMZCMNZDEFBOPZMCMUADFCMZUEMNZDEUEMFCMUFUCUEMNZKLUDAUEBCD
      EFGUEQHRUBUDUGUHST $.
      $( [27-Aug-2011] $)

    $d r u w x y z B $.  $d r u x y z R $.  $d r u w x y z T $.
    $( The unit element of a ring is unique. $)
    rngideu $p |- ( R e. Ring ->
                 E! u e. B A. x e. B ( ( u T x ) = x /\ ( x T u ) = x ) ) $=
      ( vy vz crg wcel cv co wceq wa wral wrex wmo wreu cgrp cfv w3a eqid isrng
      cplusg simp3bi mgmidmo a1i reu5 sylanbrc ) DJKZBLZALZEMUMNUMULEMUMNOACPZB
      CQZULCKUNOBRZUNBCSUKDTKUMHLZEMZCKURILZEMUMUQUSEMZEMNUMUQUSDUEUAZMEMURUMUS
      EMZVAMNUMUQVAMUSEMVBUTVAMNUBOICPHCPACPUOAHIBCVADEFVAUCGUDUFUPUKABCEUGUHUN
      BCUIUJ $.
      $( [27-Aug-2011] $)
  $}

  ${
    rngdi.b $e |- B = ( Base ` R ) $.
    rngdi.p $e |- P = ( +g ` R ) $.
    rngdi.t $e |- T = ( .r ` R ) $.
    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngdi $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                     ( X T ( Y P Z ) ) = ( ( X T Y ) P ( X T Z ) ) ) $=
      ( crg wcel w3a wa co wceq rngi simpr2 syl ) CKLEALFALGALMNEFDOZALZTGDOEFG
      DOZDOPZEFGBODOTEGDOZBOPZEFBOGDOUDUBBOPZMNUEABCDEFGHIJQUAUCUEUFRS $.
      $( [27-Aug-2011] $)

    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngdir $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                      ( ( X P Y ) T Z ) = ( ( X T Z ) P ( Y T Z ) ) ) $=
      ( crg wcel w3a wa co wceq rngi simpr3 syl ) CKLEALFALGALMNEFDOZALZTGDOEFG
      DOZDOPZEFGBODOTEGDOZBOPZEFBOGDOUDUBBOPZMNUFABCDEFGHIJQUAUCUEUFRS $.
      $( [27-Aug-2011] $)
  $}

  ${
    $d x y B $.  $d x y R $.
    rngidcl.b $e |- B = ( Base ` R ) $.
    rngidcl.u $e |- U = ( 1r ` R ) $.
    $( The unit element of a ring belongs to the base set of the ring. $)
    rngidcl $p |- ( R e. Ring -> U e. B ) $=
      ( vy vx crg wcel cmgp cfv c0g eqid rngidval cmulr mgpbas mgpplusg cv wceq
      co wa wral wreu wrex rngideu reurex syl mgmidcl eqeltrd ) BHIZCBJKZLKZABC
      UKHUKMZENUJFABOKZGUKULABUKUMDPULMBUNUKUMUNMZQUJGRZFRZUNTUQSUQUPUNTUQSUAFA
      UBZGAUCURGAUDFGABUNDUOUEURGAUFUGUHUI $.
      $( [27-Dec-2014] $) $( [27-Aug-2011] $)
  $}

  ${
    $d r u x y z R $.
    $( A ring is a group. $)
    rnggrp $p |- ( R e. Ring -> R e. Grp ) $=
      ( vx vy vz vu crg wcel cgrp cv cmulr cfv co cbs wceq cplusg w3a wral wrex
      wa eqid isrng simp1bi ) AFGAHGBIZCIZAJKZLZAMKZGUFDIZUELUCUDUHUELZUELNUCUD
      UHAOKZLUELUFUCUHUELZUJLNUCUDUJLUHUELUKUIUJLNPSDUGQCUGQBUGQEIZUCUELUCNUCUL
      UELUCNSBUGQEUGRBCDEUGUJAUEUGTUJTUETUAUB $.
      $( [15-Sep-2011] $)
  $}

  ${
    rng0cl.b $e |- B = ( Base ` R ) $.
    rng0cl.z $e |- Z = ( 0g ` R ) $.
    $( The zero element of a ring belongs to its base set. $)
    rng0cl $p |- ( R e. Ring -> Z e. B ) $=
      ( crg wcel cgrp rnggrp grpidcl syl ) BFGBHGCAGBIABCDEJK $.
      $( [12-Jan-2014] $)
  $}

  ${
    $d x y B $.  $d x y I $.  $d x y R $.  $d x y T $.  $d y X $.
    rngidm.b $e |- B = ( Base ` R ) $.
    rngidm.t $e |- T = ( .r ` R ) $.
    rngidm.u $e |- U = ( 1r ` R ) $.
    $( Lemma for ~ rnglidm and ~ rngridm . $)
    rngidmlem $p |- ( ( R e. Ring /\ X e. B )
           -> ( ( U T X ) = X /\ ( X T U ) = X ) ) $=
      ( vy vx crg wcel cmgp cfv co wceq wa eqid cv eqeq1d mgpbas wral wreu wrex
      c0g mgpplusg rngideu reurex mgmlrid rngidval oveq1d oveq2d anbi12d syldan
      syl biimpar ) BKLZEALBMNZUENZECOZEPZEUSCOZEPZQZDECOZEPZEDCOZEPZQZUQIACJUR
      USEABURURRZFUAUSRBCURVJGUFUQJSZISZCOVLPVLVKCOVLPQIAUBZJAUCVMJAUDIJABCFGUG
      VMJAUHUOUIUQVIVDUQVFVAVHVCUQVEUTEUQDUSECBDURKVJHUJZUKTUQVGVBEUQDUSECVNULT
      UMUPUN $.
      $( [27-Dec-2014] $) $( [15-Sep-2011] $)

    $( The unit element of a ring is a left multiplicative identity. $)
    rnglidm $p |- ( ( R e. Ring /\ X e. B ) -> ( U T X ) = X ) $=
      ( crg wcel wa co wceq rngidmlem simpld ) BIJEAJKDECLEMEDCLEMABCDEFGHNO $.
      $( [15-Sep-2011] $)

    $( The unit element of a ring is a right multiplicative identity. $)
    rngridm $p |- ( ( R e. Ring /\ X e. B ) -> ( X T U ) = X ) $=
      ( crg wcel wa co wceq rngidmlem simprd ) BIJEAJKDECLEMEDCLEMABCDEFGHNO $.
      $( [15-Sep-2011] $)

    $( Properties showing that an element ` I ` is the unity element of a
       ring. $)
    isrngid $p |- ( R e. Ring
      -> ( ( I e. B /\ A. x e. B ( ( I T x ) = x /\ ( x T I ) = x ) )
        <-> U = I ) ) $=
      ( vy crg wcel cv co wceq wa wral cmgp cfv eqid mgpplusg wreu wrex rngideu
      c0g mgpbas reurex syl ismgmid rngidval eqeq1d bitr4d ) CKLZFBLFAMZDNUNOUN
      FDNUNOPABQPCRSZUESZFOEFOUMABDFJUOUPBCUOUOTZGUFUPTCDUOUQHUAUMJMZUNDNUNOUNU
      RDNUNOPABQZJBUBUSJBUCAJBCDGHUDUSJBUGUHUIUMEUPFCEUOKUQIUJUKUL $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d y B $.  $d x y M $.  $d x y O $.  $d x y R $.
    rngidss.g $e |- M = ( ( mulGrp ` R ) |`s A ) $.
    rngidss.b $e |- B = ( Base ` R ) $.
    rngidss.u $e |- O = ( 1r ` R ) $.
    $( A subset of the multiplicative group has the multiplicative identity as
       its identity if the identity is in the subset.  (Contributed by Mario
       Carneiro, 27-Dec-2014.) $)
    rngidss $p |- ( ( R e. Ring /\ A C_ B /\ O e. A ) -> O = ( 0g ` M ) ) $=
      ( vy vx wcel wss cfv cv co wceq wa wral eqid eqeq1d crg w3a c0g cbs cmulr
      cmgp mgpplusg ressplusg wrex simp3 mgpbas ressbas2 3ad2ant2 eleqtrd simp2
      eqsstr3d rngidmlem ralrimiva 3ad2ant1 ssralv sylc anbi12d ralbidv rcla4ev
      oveq1 oveq2 syl2anc ismgmid mpbi2and eqcomd ) CUAKZABLZEAKZUBZDUCMZEVNEDU
      DMZKZEINZCUEMZOZVRPZVREVSOZVRPZQZIVPRZVOEPVNIVPVSEJDVOVPSVOSAVSCUFMZDFCVS
      WFWFSZVSSZUGUHVNVQWEJNZVRVSOZVRPZVRWIVSOZVRPZQZIVPRZJVPUIVNEAVPVKVLVMUJVL
      VKAVPPVMABDWFFBCWFWGGUKULUMZUNZVNVPBLWDIBRZWEVNVPABWPVKVLVMUOUPVKVLWRVMVK
      WDIBBCVSEVRGWHHUQURUSWDIVPBUTVAZWOWEJEVPWIEPZWNWDIVPWTWKWAWMWCWTWJVTVRWIE
      VRVSVETWTWLWBVRWIEVRVSVFTVBVCVDVGVHWQWSVIVJ $.
      $( [27-Dec-2014] $)
  $}

  ${
    rngacl.b $e |- B = ( Base ` R ) $.
    rngacl.p $e |- P = ( +g ` R ) $.
    $( Closure of the addition operation of a ring. $)
    rngacl $p |- ( ( R e. Ring /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( crg wcel cgrp co rnggrp grpcl syl3an1 ) CHICJIDAIEAIDEBKAICLABCDEFGMN
      $.
      $( [14-Jan-2014] $)

    $( Commutativity of the additive group of a ring.  (See also ~ lmodcom .)
       (Contributed by Mario Carneiro and G&eacute;rard Lang, 3-Dec-2014.) $)
    rngcom $p |- ( ( R e. Ring /\ X e. B /\ Y e. B ) ->
      ( X P Y ) = ( Y P X ) ) $=
      ( wcel co wceq cfv rngacl syl3anc syl13anc rngdir rnglidm syl2anc oveq12d
      eqid grpass crg w3a cur cmulr simp1 rngidcl syl simp2 simp3 rngdi 3eqtr3d
      eqtr3d eqtrd cgrp rnggrp 3eqtr4d wb grprcan mpbid 3com23 grplcan ) CUAHZD
      AHZEAHZUBZDDEBIZBIZDEDBIZBIZJZVFVHJZVEDDBIZEBIZVFDBIZVGVIVEVMEBIZVNEBIZJZ
      VMVNJZVEVLEEBIZBIZVFVFBIZVOVPVECUCKZWBBIZDCUDKZIZWCEWDIZBIZWBVFWDIZWHBIZV
      TWAVEWCVFWDIZWGWIVEVBWCAHZVCVDWJWGJVBVCVDUEZVEVBWBAHZWMWKWLVEVBWMWLACWBFW
      BSZUFUGZWOABCWBWBFGLMVBVCVDUHZVBVCVDUIZABCWDWCDEFGWDSZUJNVEVBWMWMVFAHZWJW
      IJWLWOWOABCDEFGLZABCWDWBWBVFFGWRONULVEWEVLWFVSBVEWEWBDWDIZXABIZVLVEVBWMWM
      VCWEXBJWLWOWOWPABCWDWBWBDFGWRONVEXADXADBVEVBVCXADJWLWPACWDWBDFWRWNPQZXCRU
      MVEWFWBEWDIZXDBIZVSVEVBWMWMVDWFXEJWLWOWOWQABCWDWBWBEFGWRONVEXDEXDEBVEVBVD
      XDEJWLWQACWDWBEFWRWNPQZXFRUMRVEWHVFWHVFBVEVBWSWHVFJWLWTACWDWBVFFWRWNPQZXG
      RUKVECUNHZVLAHZVDVDVOVTJVEVBXHWLCUOUGZVEVBVCVCXIWLWPWPABCDDFGLMZWQWQABCVL
      EEFGTNVEXHWSVCVDVPWAJXJWTWPWQABCVFDEFGTNUPVEXHVMAHZVNAHZVDVQVRUQXJVEVBXIV
      DXLWLXKWQABCVLEFGLMVEVBWSVCXMWLWTWPABCVFDFGLMWQABCVMVNEFGURNUSVEXHVCVCVDV
      MVGJXJWPWPWQABCDDEFGTNVEXHVCVDVCVNVIJXJWPWQWPABCDEDFGTNUKVEXHWSVHAHZVCVJV
      KUQXJWTVBVDVCXNABCEDFGLUTWPABCVFVHDFGVANUS $.
      $( [4-Dec-2014] $)
  $}

  ${
    $d x y R $.
    $( A ring is an Abelian group. $)
    rngabl $p |- ( R e. Ring -> R e. Abel ) $=
      ( vx vy crg wcel cbs cfv cplusg rnggrp eqidd cv eqid rngcom isabld ) ADEZ
      BCAFGZAHGZAAIOPJOQJPQABKCKPLQLMN $.
      $( [26-Aug-2011] $)
  $}

  ${
    $d s u v w x y B $.  $d s u v w x y K $.  $d s u v w x y ph $.
    $d s u v w x y L $.
    rngpropd.1 $e |- ( ph -> B = ( Base ` K ) ) $.
    rngpropd.2 $e |- ( ph -> B = ( Base ` L ) ) $.
    rngpropd.3 $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
      ( x ( +g ` K ) y ) = ( x ( +g ` L ) y ) ) $.
    rngpropd.4 $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
      ( x ( .r ` K ) y ) = ( x ( .r ` L ) y ) ) $.
    $( If two structures have the same group components (properties), one is a
       ring iff the other one is.  (Contributed by Mario Carneiro,
       6-Dec-2014.) $)
    rngpropd $p |- ( ph -> ( K e. Ring <-> L e. Ring ) ) $=
      ( vu vv vw co wcel wa wral wceq proplem syl12anc vs cv cplusg cfv crg cbs
      cmulr simplr simprl ad2antrr eleqtrd simprr rngacl syl3anc eleqtrrd rngcl
      eqid jca ralrimivva ex adantlr eleq12d mpbird wb cgrp w3a grppropd adantr
      simplll simplrl simplrr simpllr simpr ralimi syl proplem2 syl21anc oveq1d
      wrex eqtrd oveq2d eqeq12d simpl oveq12d 3anbi123d anbi12d ralbidva eleq2d
      2ralbidva anbi1d raleqbidv raleqdv 3bitr3d eqeq1d rexeqbidv isrng 3bitr4g
      rexbidva pm5.21ndd ) ABUBZCUBZEUCUDZNZDOZWTXAEUGUDZNZDOZPZCDQZBDQZEUEOZFU
      EOZAXKXJAXKPZXHBCDDXMWTDOZXADOZPZPZXDXGXQXCEUFUDZDXQXKWTXROZXAXROZXCXROAX
      KXPUHZXQWTDXRXMXNXOUIADXRRZXKXPGUJZUKZXQXADXRXMXNXOULYCUKZXRXBEWTXAXRUQZX
      BUQZUMUNYCUOXQXFXRDXQXKXSXTXFXROYAYDYEXREXEWTXAYFXEUQZUPUNYCUOURUSUTAXLXJ
      AXLPZXHBCDDYIXPPZXDXGYJXDWTXAFUCUDZNZFUFUDZOZYJXLWTYMOZXAYMOZYNAXLXPUHZYJ
      WTDYMYIXNXOUIADYMRZXLXPHUJZUKZYJXADYMYIXNXOULYSUKZYMYKFWTXAYMUQZYKUQZUMUN
      YJXCYLDYMAXPXCYLRXLIVAYSVBVCYJXGWTXAFUGUDZNZYMOZYJXLYOYPUUFYQYTUUAYMFUUDW
      TXAUUBUUDUQZUPUNYJXFUUEDYMAXPXFUUERXLJVAYSVBVCURUSUTAXJXKXLVDAXJPZEVEOZKU
      BZLUBZXENZXROZUULMUBZXENZUUJUUKUUNXENZXENZRZUUJUUKUUNXBNZXENZUULUUJUUNXEN
      ZXBNZRZUUJUUKXBNZUUNXENZUVAUUPXBNZRZVFZPZMXRQZLXRQZKXRQZUAUBZUUJXENZUUJRZ
      UUJUVMXENZUUJRZPZKXRQZUAXRVSZVFFVEOZUUJUUKUUDNZYMOZUWBUUNUUDNZUUJUUKUUNUU
      DNZUUDNZRZUUJUUKUUNYKNZUUDNZUWBUUJUUNUUDNZYKNZRZUUJUUKYKNZUUNUUDNZUWJUWEY
      KNZRZVFZPZMYMQZLYMQZKYMQZUVMUUJUUDNZUUJRZUUJUVMUUDNZUUJRZPZKYMQZUAYMVSZVF
      XKXLUUHUUIUWAUVLUXAUVTUXHAUUIUWAVDXJABCDEFGHIVGVHUUHUULDOZUVHPZMDQZLDQZKD
      QUWRMDQZLDQZKDQUVLUXAUUHUXKUXMKLDDUUHUUJDOZUUKDOZPZPZUXJUWRMDUXRUUNDOZPZU
      XIUWCUVHUWQUXTUULUWBDYMUXTAUXOUXPUULUWBRAXJUXQUXSVIZUUHUXOUXPUXSVJZUUHUXO
      UXPUXSVKZABCDDXEUUDUUJUUKJSTZUUHYRUXQUXSAYRXJHVHZUJVBUXTUURUWGUVCUWLUVGUW
      PUXTUUOUWDUUQUWFUXTUUOUULUUNUUDNZUWDUXTAUXIUXSUUOUYFRUYAUXTUXOUXPXGCDQZBD
      QZUXIUYBUYCUXTXJUYHAXJUXQUXSVLZXIUYGBDXHXGCDXDXGVMVNVNVOZBCDXEUUJUUKVPVQZ
      UXRUXSVMZABCDDXEUUDUULUUNJSTUXTUULUWBUUNUUDUYDVRVTUXTUUQUUJUUPUUDNZUWFUXT
      AUXOUUPDOZUUQUYMRUYAUYBUXTUXPUXSUYHUYNUYCUYLUYJBCDXEUUKUUNVPVQZABCDDXEUUD
      UUJUUPJSTUXTUUPUWEUUJUUDUXTAUXPUXSUUPUWERUYAUYCUYLABCDDXEUUDUUKUUNJSTZWAV
      TWBUXTUUTUWIUVBUWKUXTUUTUUJUUSUUDNZUWIUXTAUXOUUSDOZUUTUYQRUYAUYBUXTUXPUXS
      XDCDQZBDQZUYRUYCUYLUXTXJUYTUYIXIUYSBDXHXDCDXDXGWCVNVNVOZBCDXBUUKUUNVPVQAB
      CDDXEUUDUUJUUSJSTUXTUUSUWHUUJUUDUXTAUXPUXSUUSUWHRUYAUYCUYLABCDDXBYKUUKUUN
      ISTWAVTUXTUVBUULUVAYKNZUWKUXTAUXIUVADOZUVBVUBRUYAUYKUXTUXOUXSUYHVUCUYBUYL
      UYJBCDXEUUJUUNVPVQZABCDDXBYKUULUVAISTUXTUULUWBUVAUWJYKUYDUXTAUXOUXSUVAUWJ
      RUYAUYBUYLABCDDXEUUDUUJUUNJSTZWDVTWBUXTUVEUWNUVFUWOUXTUVEUVDUUNUUDNZUWNUX
      TAUVDDOZUXSUVEVUFRUYAUXTUXOUXPUYTVUGUYBUYCVUABCDXBUUJUUKVPVQUYLABCDDXEUUD
      UVDUUNJSTUXTUVDUWMUUNUUDUXTAUXOUXPUVDUWMRUYAUYBUYCABCDDXBYKUUJUUKISTVRVTU
      XTUVFUVAUUPYKNZUWOUXTAVUCUYNUVFVUHRUYAVUDUYOABCDDXBYKUVAUUPISTUXTUVAUWJUU
      PUWEYKVUEUYPWDVTWBWEWFWGWIUUHUXLUVKKDXRAYBXJGVHZUUHUXKUVJLDXRVUIUUHUXJUVI
      MDXRVUIUUHUXIUUMUVHUUHDXRUULVUIWHWJWKWKWKUUHUXNUWTKDYMUYEUUHUXMUWSLDYMUYE
      UUHUWRMDYMUYEWLWKWKWMUUHUVRKDQZUADVSUXFKDQZUADVSUVTUXHUUHVUJVUKUADUUHUVMD
      OZPZUVRUXFKDVUMUXOPZUVOUXCUVQUXEVUNUVNUXBUUJVUNAVULUXOUVNUXBRAXJVULUXOVIZ
      UUHVULUXOUHZVUMUXOVMZABCDDXEUUDUVMUUJJSTWNVUNUVPUXDUUJVUNAUXOVULUVPUXDRVU
      OVUQVUPABCDDXEUUDUUJUVMJSTWNWFWGWRUUHVUJUVSUADXRVUIUUHUVRKDXRVUIWLWOUUHVU
      KUXGUADYMUYEUUHUXFKDYMUYEWLWOWMWEKLMUAXRXBEXEYFYGYHWPKLMUAYMYKFUUDUUBUUCU
      UGWPWQUTWS $.
      $( [6-Dec-2014] $)
  $}

  ${
    $d x y K $.  $d x y L $.
    rngprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    rngprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    rngprop.m $e |- ( .r ` K ) = ( .r ` L ) $.
    $( If two structures have the same ring components (properties), one is a
       ring iff the other one is. $)
    rngprop $p |- ( K e. Ring <-> L e. Ring ) $=
      ( vx vy crg wcel wtru cbs cfv wceq a1i cv cplusg co wa oveqi cmulr eqidd
      wb rngpropd trud ) AHIBHIUBJFGAKLZABJUEUAUEBKLMJCNFOZGOZAPLZQUFUGBPLZQMJU
      FUEIUGUEIRRZUHUIUFUGDSNUFUGATLZQUFUGBTLZQMUJUKULUFUGESNUCUD $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d u x y z B $.  $d u x y z R $.  $d u T $.  $d u x U $.  $d u x y z ph $.
    isrngd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isrngd.p $e |- ( ph -> P = ( +g ` R ) ) $.
    isrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isrngd.g $e |- ( ph -> R e. Grp ) $.
    isrngd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x T y ) e. B ) $.
    isrngd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                  ( ( x T y ) T z ) = ( x T ( y T z ) ) ) $.
    isrngd.d $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                  ( x T ( y P z ) ) = ( ( x T y ) P ( x T z ) ) ) $.
    isrngd.e $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                  ( ( x P y ) T z ) = ( ( x T z ) P ( y T z ) ) ) $.
    isrngd.u $e |- ( ph -> U e. B ) $.
    isrngd.i $e |- ( ( ph /\ x e. B ) -> ( U T x ) = x ) $.
    isrngd.h $e |- ( ( ph /\ x e. B ) -> ( x T U ) = x ) $.
    $( Properties that determine a ring. $)
    isrngd $p |- ( ph -> R e. Ring ) $=
      ( vu cgrp wcel cv cmulr cfv co cbs wceq cplusg w3a wa wral wrex 3adant3r3
      crg 3jca ralrimivvva ralrimiva oveq1 eqeq1d oveq2 anbi12d ralbidv rcla4ev
      syl2anc oveqd eleq12d eqidd oveq123d eqeq12d 3anbi123d raleqbidv 3anbi23d
      jca rexeqbidv mpbid eqid isrng sylibr ) AGUBUCZBUDZCUDZGUEUFZUGZGUHUFZUCZ
      WEDUDZWDUGZWBWCWHWDUGZWDUGZUIZWBWCWHGUJUFZUGZWDUGZWEWBWHWDUGZWMUGZUIZWBWC
      WMUGZWHWDUGZWPWJWMUGZUIZUKZULZDWFUMZCWFUMZBWFUMZUAUDZWBWDUGZWBUIZWBXHWDUG
      ZWBUIZULZBWFUMZUAWFUNZUKZGUPUCAWAWBWCHUGZEUCZXQWHHUGZWBWCWHHUGZHUGZUIZWBW
      CWHFUGZHUGZXQWBWHHUGZFUGZUIZWBWCFUGZWHHUGZYEXTFUGZUIZUKZULZDEUMZCEUMZBEUM
      ZXHWBHUGZWBUIZWBXHHUGZWBUIZULZBEUMZUAEUNZUKXPAWAYPUUCMAYMBCDEEEAWBEUCZWCE
      UCZWHEUCZUKULZXRYLAUUDUUEXRUUFNUOUUGYBYGYKOPQUQVOURAIEUCIWBHUGZWBUIZWBIHU
      GZWBUIZULZBEUMZUUCRAUULBEAUUDULUUIUUKSTVOUSUUBUUMUAIEXHIUIZUUAUULBEUUNYRU
      UIYTUUKUUNYQUUHWBXHIWBHUTVAUUNYSUUJWBXHIWBHVBVAVCVDVEVFUQAYPXGUUCXOWAAYOX
      FBEWFJAYNXECEWFJAYMXDDEWFJAXRWGYLXCAXQWEEWFAHWDWBWCLVGZJVHAYBWLYGWRYKXBAX
      SWIYAWKAXQWEWHWHHWDLUUOAWHVIZVJAWBWBXTWJHWDLAWBVIZAHWDWCWHLVGZVJVKAYDWOYF
      WQAWBWBYCWNHWDLUUQAFWMWCWHKVGVJAXQWEYEWPFWMKUUOAHWDWBWHLVGZVJVKAYIWTYJXAA
      YHWSWHWHHWDLAFWMWBWCKVGUUPVJAYEWPXTWJFWMKUUSUURVJVKVLVCVMVMVMAUUBXNUAEWFJ
      AUUAXMBEWFJAYRXJYTXLAYQXIWBAHWDXHWBLVGVAAYSXKWBAHWDWBXHLVGVAVCVMVPVNVQBCD
      UAWFWMGWDWFVRWMVRWDVRVSVT $.
      $( [2-Aug-2013] $)
  $}

  ${
    rngz.b $e |- B = ( Base ` R ) $.
    rngz.t $e |- T = ( .r ` R ) $.
    rngz.z $e |- Z = ( 0g ` R ) $.
    $( The zero of a unital ring is a left absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    rnglz $p |- ( ( R e. Ring /\ X e. B ) -> ( Z T X ) = Z ) $=
      ( crg wcel wa co cplusg cfv wceq cgrp rnggrp grpidcl syl adantr mpdan w3a
      eqid grplid oveq1d simpr rngdir syldan simpl rngcl syl3anc grprid syl2anc
      3jca eqcomd 3eqtr3d wb grplcan syl13anc mpbid ) BIJZDAJZKZEDCLZVDBMNZLZVD
      EVELZOZVDEOZVCEEVELZDCLZVDVFVGVCVJEDCVAVJEOZVBVABPJZVLBQZVMEAJZVLABEFHRZA
      VEBEEFVEUCZHUDUASTUEVAVBVOVOVBUBVKVFOVCVOVOVBVAVOVBVAVMVOVNVPSTZVRVAVBUFZ
      UNAVEBCEEDFVQGUGUHVCVMVDAJZVDVGOVAVMVBVNTZVCVAVOVBVTVAVBUIVRVSABCEDFGUJUK
      ZVMVTKVGVDAVEBEVDFVQHULUOUMUPVCVMVTVOVTVHVIUQWAWBVRWBAVEBVDEVDFVQURUSUT
      $.
      $( [15-Sep-2011] $)

    $( The zero of a unital ring is a right absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    rngrz $p |- ( ( R e. Ring /\ X e. B ) -> ( X T Z ) = Z ) $=
      ( crg wcel wa co cplusg cfv wceq cgrp rnggrp grplid syl adantr eqid mpdan
      grpidcl oveq2d w3a simpr 3jca rngdi syldan mpd3an3 eqcomd syl2anc 3eqtr3d
      rngcl wb grprcan syl13anc mpbid ) BIJZDAJZKZDECLZVBBMNZLZEVBVCLZOZVBEOZVA
      DEEVCLZCLZVBVDVEVAVHEDCUSVHEOZUTUSBPJZVJBQZVKEAJZVJABEFHUCZAVCBEEFVCUAZHR
      UBSTUDUSUTUTVMVMUEVIVDOVAUTVMVMUSUTUFUSVMUTUSVKVMVLVNSTZVPUGAVCBCDEEFVOGU
      HUIVAVKVBAJZVBVEOUSVKUTVLTZUSUTVMVQVPABCDEFGUNUJZVKVQKVEVBAVCBEVBFVOHRUKU
      LUMVAVKVQVMVQVFVGUOVRVSVPVSAVCBVBEVBFVOUPUQUR $.
      $( [15-Sep-2011] $)
  $}

  ${
    rng1eq0.b $e |- B = ( Base ` R ) $.
    rng1eq0.u $e |- U = ( 1r ` R ) $.
    rng1eq0.z $e |- Z = ( 0g ` R ) $.
    $( If one and zero are equal, then any two elements of a ring are equal.
       Alternatively, every ring has one distinct from zero except the zero
       ring containing the single element ` { 0 } ` .  (Contributed by Mario
       Carneiro, 9-Sep-2014.) $)
    rng1eq0 $p |- ( ( R e. Ring /\ X e. B /\ Y e. B ) ->
      ( U = Z -> X = Y ) ) $=
      ( crg wcel w3a wceq wa cmulr co rnglz syl2anc oveq1d rnglidm simpl1 simpr
      cfv simpl2 eqid simpl3 eqtr4d 3eqtr4d 3eqtr3d ex ) BJKZDAKZEAKZLZCFMZDEMU
      NUONZCDBOUCZPZCEUQPZDEUPFDUQPZFEUQPZURUSUPUTFVAUPUKULUTFMUKULUMUOUAZUKULU
      MUOUDZABUQDFGUQUEZIQRUPUKUMVAFMVBUKULUMUOUFZABUQEFGVDIQRUGUPCFDUQUNUOUBZS
      UPCFEUQVFSUHUPUKULURDMVBVCABUQCDGVDHTRUPUKUMUSEMVBVEABUQCEGVDHTRUIUJ $.
      $( [10-Sep-2014] $)
  $}

  ${
    rngneg1.b $e |- B = ( Base ` R ) $.
    rngneg1.t $e |- T = ( .r ` R ) $.
    rngneg1.u $e |- U = ( 1r ` R ) $.
    rngneg1.m $e |- N = ( invg ` R ) $.
    $( Minus 1 times a ring element is its negative.  Equation 2 of [Kreyszig]
       p. 51.  ( ~ mulm1 analog.)  Use ~ rngnegl instead.  Obsolete as of
       2-Jul-2014. $)
    rngneg1OLD $p |- ( ( R e. Ring /\ X e. B )
         -> ( ( N ` U ) T X ) = ( N ` X ) ) $=
      ( wcel cfv co wceq adantr grpinvcl syl2anc sylan eqid syl13anc crg wa c0g
      cplusg cgrp rnggrp simpl rngidcl simpr rngcl syl3anc grpass oveq2d oveq1d
      rnglidm grplinv rngdir 3eqtr3d eqtr3d grprinv grplid grprid 3eqtr3rd
      rnglz ) BUAKZFAKZUBZBUCLZFELZBUDLZMZDELZFCMZVHVJMZVIVMVGVMFVJMZVIVJMZVMFV
      IVJMZVJMZVKVNVGBUEKZVMAKZVFVIAKZVPVRNVEVSVFBUFZOZVGVEVLAKZVFVTVEVFUGZVEWD
      VFVEVSDAKZWDWBABDGIUHZABEDGJPQOZVEVFUIZABCVLFGHUJUKZWIVEVSVFWAWBABEFGJPRZ
      AVJBVMFVIGVJSZULTVGVOVHVIVJVGVMDFCMZVJMZVOVHVGWMFVMVJABCDFGHIUOUMVGVLDVJM
      ZFCMZVHFCMWNVHVGWOVHFCVGVSWFWOVHNWCVEWFVFWGOZAVJBEVHDGWLVHSZJUPQUNVGVEWDW
      FVFWPWNNWEWHWQWIAVJBCVLDFGWLHUQTABCFVHGHWRVDURUSUNVGVQVHVMVJVEVSVFVQVHNWB
      AVJBEVHFGWLWRJUTRUMURVGVSWAVKVINWCWKAVJBVHVIGWLWRVAQVGVSVTVNVMNWCWJAVJBVH
      VMGWLWRVBQVC $.
      $( [18-Apr-2014] $)
  $}

  ${
    rngnegl.b $e |- B = ( Base ` R ) $.
    rngnegl.t $e |- T = ( .r ` R ) $.
    rngnegl.u $e |- U = ( 1r ` R ) $.
    rngnegl.n $e |- N = ( invg ` R ) $.
    rngnegl.r $e |- ( ph -> R e. Ring ) $.
    rngnegl.x $e |- ( ph -> X e. B ) $.
    $( Negation in a ring is the same as left multiplication by -1.
       ( ~ rngonegmn1l analog.)  (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    rngnegl $p |- ( ph -> ( ( N ` U ) T X ) = ( N ` X ) ) $=
      ( cfv co wceq wcel syl syl2anc eqid cplusg c0g rngidcl cgrp rnggrp rngdir
      crg grpinvcl syl13anc grprinv oveq1d rnglz eqtrd rnglidm 3eqtr3rd syl3anc
      wb rngcl grpinvid1 mpbird eqcomd ) AGFNZEFNZGDOZAVBVDPZGVDCUANZOZCUBNZPZA
      EVCVFOZGDOZEGDOZVDVFOZVHVGACUGQZEBQZVCBQZGBQZVKVMPLAVNVOLBCEHJUCRZACUDQZV
      OVPAVNVSLCUERZVRBCFEHKUHSZMBVFCDEVCGHVFTZIUFUIAVKVHGDOZVHAVJVHGDAVSVOVJVH
      PVTVRBVFCFVHEHWBVHTZKUJSUKAVNVQWCVHPLMBCDGVHHIWDULSUMAVLGVDVFAVNVQVLGPLMB
      CDEGHIJUNSUKUOAVSVQVDBQZVEVIUQVTMAVNVPVQWELWAMBCDVCGHIURUPBVFCFVHGVDHWBWD
      KUSUPUTVA $.
      $( [2-Jul-2014] $)

    $( Negation in a ring is the same as right multiplication by -1.
       ( ~ rngonegmn1r analog.)  (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    rngnegr $p |- ( ph -> ( X T ( N ` U ) ) = ( N ` X ) ) $=
      ( cfv co wceq wcel syl syl2anc eqid cplusg c0g cgrp rnggrp grpinvcl rngdi
      crg rngidcl syl13anc grplinv oveq2d rngrz eqtrd rngridm 3eqtr3rd wb rngcl
      syl3anc grpinvid2 mpbird eqcomd ) AGFNZGEFNZDOZAVBVDPZVDGCUANZOZCUBNZPZAG
      VCEVFOZDOZVDGEDOZVFOZVHVGACUGQZGBQZVCBQZEBQZVKVMPLMACUCQZVQVPAVNVRLCUDRZA
      VNVQLBCEHJUHRZBCFEHKUESZVTBVFCDGVCEHVFTZIUFUIAVKGVHDOZVHAVJVHGDAVRVQVJVHP
      VSVTBVFCFVHEHWBVHTZKUJSUKAVNVOWCVHPLMBCDGVHHIWDULSUMAVLGVDVFAVNVOVLGPLMBC
      DEGHIJUNSUKUOAVRVOVDBQZVEVIUPVSMAVNVOVPWELMWABCDGVCHIUQURBVFCFVHGVDHWBWDK
      USURUTVA $.
      $( [2-Jul-2014] $)
  $}

  ${
    rngneglmul.b $e |- B = ( Base ` R ) $.
    rngneglmul.t $e |- T = ( .r ` R ) $.
    rngneglmul.n $e |- N = ( invg ` R ) $.
    rngneglmul.r $e |- ( ph -> R e. Ring ) $.
    rngneglmul.x $e |- ( ph -> X e. B ) $.
    rngneglmul.y $e |- ( ph -> Y e. B ) $.
    $( Negation of a product in a ring.  ( ~ mulneg1 analog.)  (Contributed by
       Jeff Madsen, 19-Jun-2010.) $)
    rngmneg1 $p |- ( ph -> ( ( N ` X ) T Y ) = ( N ` ( X T Y ) ) ) $=
      ( cur cfv co crg wcel syl rngnegl wceq cgrp eqid rngidcl grpinvcl syl2anc
      rnggrp rngass syl13anc oveq1d rngcl syl3anc 3eqtr3d ) ACNOZEOZFDPZGDPZUOF
      GDPZDPZFEOZGDPUREOACQRZUOBRZFBRZGBRZUQUSUAKACUBRZUNBRZVBAVAVEKCUGSAVAVFKB
      CUNHUNUCZUDSBCEUNHJUEUFLMBCDUOFGHIUHUIAUPUTGDABCDUNEFHIVGJKLTUJABCDUNEURH
      IVGJKAVAVCVDURBRKLMBCDFGHIUKULTUM $.
      $( [2-Jul-2014] $)

    $( Negation of a product in a ring.  ( ~ mulneg2 analog.)  (Contributed by
       Jeff Madsen, 19-Jun-2010.) $)
    rngmneg2 $p |- ( ph -> ( X T ( N ` Y ) ) = ( N ` ( X T Y ) ) ) $=
      ( co cur cfv crg wcel syl rngnegr wceq cgrp eqid rngidcl grpinvcl syl2anc
      rnggrp rngass syl13anc rngcl syl3anc oveq2d 3eqtr3rd ) AFGDNZCOPZEPZDNZFG
      UPDNZDNZUNEPFGEPZDNACQRZFBRZGBRZUPBRZUQUSUAKLMACUBRZUOBRZVDAVAVEKCUGSAVAV
      FKBCUOHUOUCZUDSBCEUOHJUEUFBCDFGUPHIUHUIABCDUOEUNHIVGJKAVAVBVCUNBRKLMBCDFG
      HIUJUKTAURUTFDABCDUOEGHIVGJKMTULUM $.
      $( [2-Jul-2014] $)

    $( Double negation of a product in a ring.  ( ~ mul2neg analog.)
       (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    rngm2neg $p |- ( ph -> ( ( N ` X ) T ( N ` Y ) ) = ( X T Y ) ) $=
      ( cfv co cgrp wcel crg rnggrp syl2anc syl grpinvcl rngmneg1 rngmneg2 wceq
      fveq2d rngcl syl3anc grpinvinv 3eqtrd ) AFENGENZDOFUKDOZENFGDOZENZENZUMAB
      CDEFUKHIJKLACPQZGBQZUKBQACRQZUPKCSUAZMBCEGHJUBTUCAULUNEABCDEFGHIJKLMUDUFA
      UPUMBQZUOUMUEUSAURFBQUQUTKLMBCDFGHIUGUHBCEUMHJUITUJ $.
      $( [4-Dec-2014] $)
  $}

  ${
    rngsubdi.b $e |- B = ( Base ` R ) $.
    rngsubdi.t $e |- T = ( .r ` R ) $.
    rngsubdi.m $e |- M = ( -g ` R ) $.
    rngsubdi.r $e |- ( ph -> R e. Ring ) $.
    rngsubdi.x $e |- ( ph -> X e. B ) $.
    rngsubdi.y $e |- ( ph -> Y e. B ) $.
    rngsubdi.z $e |- ( ph -> Z e. B ) $.
    $( Ring multiplication distributes over subtraction.  ( ~ subdi analog.)
       (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    rngsubdi $p |- ( ph -> ( X T ( Y M Z ) ) = ( ( X T Y ) M ( X T Z ) ) ) $=
      ( cfv co wcel wceq syl2anc cminusg cplusg cgrp rnggrp eqid grpinvcl rngdi
      crg syl syl13anc rngmneg2 oveq2d eqtrd grpsubval rngcl syl3anc 3eqtr4d )
      AFGHCUAPZPZCUBPZQZDQZFGDQZFHDQZURPZUTQZFGHEQZDQVCVDEQZAVBVCFUSDQZUTQZVFAC
      UHRZFBRZGBRZUSBRZVBVJSLMNACUCRZHBRZVNAVKVOLCUDUIOBCURHIURUEZUFTBUTCDFGUSI
      UTUEZJUGUJAVIVEVCUTABCDURFHIJVQLMOUKULUMAVGVAFDAVMVPVGVASNOBUTCUREGHIVRVQ
      KUNTULAVCBRZVDBRZVHVFSAVKVLVMVSLMNBCDFGIJUOUPAVKVLVPVTLMOBCDFHIJUOUPBUTCU
      REVCVDIVRVQKUNTUQ $.
      $( [2-Jul-2014] $)

    $( Ring multiplication distributes over subtraction.  ( ~ subdir analog.)
       (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    rngsubdir $p |- ( ph -> ( ( X M Y ) T Z ) = ( ( X T Z ) M ( Y T Z ) ) ) $=
      ( cfv co wcel wceq syl2anc cminusg cplusg crg cgrp rnggrp syl eqid rngdir
      grpinvcl syl13anc rngmneg1 oveq2d eqtrd grpsubval oveq1d syl3anc 3eqtr4d
      rngcl ) AFGCUAPZPZCUBPZQZHDQZFHDQZGHDQZUSPZVAQZFGEQZHDQVDVEEQZAVCVDUTHDQZ
      VAQZVGACUCRZFBRZUTBRZHBRZVCVKSLMACUDRZGBRZVNAVLVPLCUEUFNBCUSGIUSUGZUITOBV
      ACDFUTHIVAUGZJUHUJAVJVFVDVAABCDUSGHIJVRLNOUKULUMAVHVBHDAVMVQVHVBSMNBVACUS
      EFGIVSVRKUNTUOAVDBRZVEBRZVIVGSAVLVMVOVTLMOBCDFHIJURUPAVLVQVOWALNOBCDGHIJU
      RUPBVACUSEVDVEIVSVRKUNTUQ $.
      $( [2-Jul-2014] $)
  $}

  ${
    $d k x y z A $.  $d k x y z B $.  $d x y z R $.  $d x y z X $.  $d y z Z $.
    $d k x y z ph $.  $d k x y z T $.  $d k x y z Y $.
    gsummulc1.b $e |- B = ( Base ` R ) $.
    gsummulc1.z $e |- Z = ( 0g ` R ) $.
    gsummulc1.p $e |- P = ( +g ` R ) $.
    gsummulc1.t $e |- T = ( .r ` R ) $.
    gsummulc1.r $e |- ( ph -> R e. Ring ) $.
    gsummulc1.a $e |- ( ph -> A e. V ) $.
    gsummulc1.y $e |- ( ph -> Y e. B ) $.
    gsummulc1.x $e |- ( ( ph /\ k e. A ) -> X e. B ) $.
    gsummulc1.n $e |- ( ph ->
      ( `' ( k e. A |-> X ) " ( _V \ { Z } ) ) e. Fin ) $.
    $( A finite ring sum multiplied by a constant.  (Contributed by Mario
       Carneiro, 19-Dec-2014.) $)
    gsummulc1 $p |- ( ph -> ( R gsum ( k e. A |-> ( X T Y ) ) ) =
      ( ( R gsum ( k e. A |-> X ) ) T Y ) ) $=
      ( vx vy vz cv co cmpt ccom cgsu cfv cplusg eqid crg wcel cabel rngabl syl
      wa adantr simpr rngcl syl3anc fmptd wceq simprl simprr rngdir cgrp rnggrp
      syl13anc grpcl 3expb sylan oveq1 ovex fvmpt adantl 3eqtr4d gsumhomo eqidd
      oveqan12d fmptco oveq2d gsumcl 3eqtr3d ) AEUACUAUDZJFUEZUFZGBIUFZUGZUHUEE
      WHUHUEZWGUIZEGBIJFUEZUFZUHUEWJJFUEZAUBUCBCCEUJUIZWOWHEEWGHKLLMWOUKZWPAEUL
      UMZEUNUMPEUOUPZWRQAUACWFCWGAWECUMZUQWQWSJCUMZWFCUMAWQWSPURAWSUSAWTWSRURCE
      FWEJLOUTVAWGUKZVBAUBUDZCUMZUCUDZCUMZUQZUQZXBXDWOUEZJFUEZXBJFUEZXDJFUEZWOU
      EZXHWGUIZXBWGUIZXDWGUIZWOUEZXGWQXCXEWTXIXLVCAWQXFPURAXCXEVDAXCXEVEAWTXFRU
      RCWOEFXBXDJLWPOVFVIXGXHCUMZXMXIVCAEVGUMZXFXQAWQXRPEVHUPXRXCXEXQCWOEXBXDLW
      PVJVKVLUAXHWFXICWGWEXHJFVMXAXHJFVNVOUPXFXPXLVCAXCXEXNXJXOXKWOUAXBWFXJCWGW
      EXBJFVMXAXBJFVNVOUAXDWFXKCWGWEXDJFVMXAXDJFVNVOVTVPVQAGBICWHSWHUKVBZTVRAWI
      WMEUHAGUABCIWFWLWHWGSAWHVSAWGVSWEIJFVMWAWBAWJCUMWKWNVCABCWHEHKLMWRQXSTWCU
      AWJWFWNCWGWEWJJFVMXAWJJFVNVOUPWD $.
      $( [19-Dec-2014] $)

    $( A finite ring sum multiplied by a constant.  (Contributed by Mario
       Carneiro, 19-Dec-2014.) $)
    gsummulc2 $p |- ( ph -> ( R gsum ( k e. A |-> ( Y T X ) ) ) =
      ( Y T ( R gsum ( k e. A |-> X ) ) ) ) $=
      ( vx vy vz cv co cmpt ccom cgsu cfv cplusg eqid crg wcel cabel rngabl syl
      wa wi idi jca rngcl 3expa sylan fmptd adantr simprl simprr rngdi syl13anc
      wceq cgrp rnggrp grpcl 3expb oveq2 ovex oveqan12d adantl 3eqtr4d gsumhomo
      fvmpt a1i fmptco oveq2d gsumcl 3eqtr3d ) AEUACJUAUDZFUEZUFZGBIUFZUGZUHUEE
      WJUHUEZWIUIZEGBJIFUEZUFZUHUEJWLFUEZAUBUCBCCEUJUIZWQWJEEWIHKLLMWQUKZWRAEUL
      UMZEUNUMPEUOUPZWTQAUACWHCWIAWSJCUMZUQWGCUMZWHCUMZAWSXAAWSURPUSRUTWSXAXBXC
      CEFJWGLOVAVBVCWIUKZVDAUBUDZCUMZUCUDZCUMZUQZUQZJXEXGWQUEZFUEZJXEFUEZJXGFUE
      ZWQUEZXKWIUIZXEWIUIZXGWIUIZWQUEZXJWSXAXFXHXLXOVJAWSXIPVEAXAXIRVEAXFXHVFAX
      FXHVGCWQEFJXEXGLWROVHVIXJXKCUMZXPXLVJAEVKUMZXIXTAWSYAPEVLUPYAXFXHXTCWQEXE
      XGLWRVMVNVCUAXKWHXLCWIWGXKJFVOXDJXKFVPWAUPXIXSXOVJAXFXHXQXMXRXNWQUAXEWHXM
      CWIWGXEJFVOXDJXEFVPWAUAXGWHXNCWIWGXGJFVOXDJXGFVPWAVQVRVSAGBICWJSWJUKZVDZT
      VTAWKWOEUHAGUABCIWHWNWJWISWJWJVJAYBWBWIWIVJAXDWBWGIJFVOWCWDAWLCUMWMWPVJAB
      CWJEHKLMWTQYCTWEUAWLWHWPCWIWGWLJFVOXDJWLFVPWAUPWF $.
      $( [19-Dec-2014] $)
  $}

