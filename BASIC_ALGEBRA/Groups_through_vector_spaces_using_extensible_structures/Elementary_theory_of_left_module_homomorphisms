$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Elementary theory of left module homomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LMHom $.

  $( Extend class notation with the generator of left module hom-sets. $)
  clmhm $a class LMHom $.

  ${
    $d t x B $.  $d f s t w x y F $.  $d t K $.  $d s t x y S $.  $d t x y T $.
    $d t y E $.  $d t L $.  $d t M $.  $d t N $.
    $( A homomorphism of left modules is a group homomorphism which
       additionally preserves the scalar product.  This requires both
       structures to be left modules over the same ring. $)
    df-lmhm $a |- LMHom = ( s e. LMod , t e. LMod |-> { f e. ( s GrpHom t ) |
        [ ( Scalar ` s ) / w ] ( ( Scalar ` t ) = w /\
          A. x e. ( Base ` w ) A. y e. ( Base ` s )
          ( f ` ( x ( vsca ` s ) y ) ) = ( x ( vsca ` t ) ( f ` y ) ) ) } ) $.

    $( Lemma for module homomorphisms. $)
    reldmlmhm $p |- Rel dom LMHom $=
      ( vs vt vw vx vy vf clmod cv csca cfv wceq cvsca co cbs wral wa wsbc cghm
      crab clmhm df-lmhm reldmmpt2 ) ABGGBHZIJCHZKDHZEHZAHZLJMFHZJUEUFUHJUCLJMK
      EUGNJODUDNJOPCUGIJQFUGUCRMSTDECBFAUAUB $.
      $( [31-Dec-2014] $)

    islmhm.k $e |- K = ( Scalar ` S ) $.
    islmhm.l $e |- L = ( Scalar ` T ) $.
    islmhm.b $e |- B = ( Base ` K ) $.
    islmhm.e $e |- E = ( Base ` S ) $.
    islmhm.m $e |- M = ( vsca ` S ) $.
    islmhm.n $e |- N = ( vsca ` T ) $.
    $( Property of being a homomorphism of left modules.  (Contributed by
       Stefan O'Rear, 1-Jan-2015.) $)
    islmhm $p |- ( T e. V -> ( F e. ( S LMHom T ) <->
        ( ( S e. LMod /\ T e. LMod ) /\ ( F e. ( S GrpHom T ) /\ L = K /\
          A. x e. B A. y e. E ( F ` ( x M y ) ) = ( x N ( F ` y ) ) ) ) ) ) $=
      ( wcel cfv vs vt vw vf clmhm co clmod wa cghm wceq cv wral w3a wn c0 noel
      cvv reldmlmhm ovprc1 eleq2d mtbiri con4i elex anim12i ad2antrl csca cvsca
      ancoms cbs wb oveq1 eleq1 anbi1d fveq2 eqeq2d fveq2d oveqd eqeq1d anbi12d
      raleqbidv bibi12d oveq2 anbi2d 2ralbidv 3anass eqeq12i fveq2i eqtri oveqi
      raleqi ralbii bitri anbi12i anbi2i syl6bbr cop df-ov eleq2i biimpi elfvdm
      cxp cdm wsbc crab df-lmhm ovex rabex dmmpt2 3syl vex opelxp sylib ovmpt4g
      mp3an3 fvex eqeq2 raleqdv sbcie fveq1 oveq2d eqeq12d syl5bb elrab biadan2
      syl6bb vtocl2g pm5.21nd ) ELSZGDEUEUFZSZDUGSZEUGSZUHZGDEUIUFZSZIHUJZAUKZB
      UKZJUFZGTZYQYRGTZKUFZUJZBFULZACULZUMZUHZDUQSZEUQSZUHZYJYHUUJYJUUHYHUUIUUH
      YJUUHUNZYJGUOSGUPUUKYIUOGDEUEURUSUTVAVBELVCVDVHYMUUJYHUUFYKUUHYLUUIDUGVCE
      UGVCVDVEGUAUKZUBUKZUEUFZSZUULUGSZUUMUGSZUHZGUULUUMUIUFZSZUUMVFTZUULVFTZUJ
      ZYQYRUULVGTZUFZGTZYQUUAUUMVGTZUFZUJZBUULVITZULZAUVBVITZULZUHZUHZUHZVJGDUU
      MUEUFZSZYKUUQUHZGDUUMUIUFZSZUVADVFTZUJZYQYRDVGTZUFZGTZUVHUJZBDVITZULZAUWB
      VITZULZUHZUHZUHZVJYJUUGVJUAUBDEUQUQUULDUJZUUOUVRUVPUWNUWOUUNUVQGUULDUUMUE
      VKUTUWOUURUVSUVOUWMUWOUUPYKUUQUULDUGVLVMUWOUUTUWAUVNUWLUWOUUSUVTGUULDUUMU
      IVKUTUWOUVCUWCUVMUWKUWOUVBUWBUVAUULDVFVNZVOUWOUVKUWIAUVLUWJUWOUVBUWBVIUWP
      VPUWOUVIUWGBUVJUWHUULDVIVNUWOUVFUWFUVHUWOUVEUWEGUWOUVDUWDYQYRUULDVGVNVQVP
      VRVTVTVSVSVSWAUUMEUJZUVRYJUWNUUGUWQUVQYIGUUMEDUEWBUTUWQUVSYMUWMUUFUWQUUQY
      LYKUUMEUGVLWCUWQUWMYOEVFTZUWBUJZUWFYQUUAEVGTZUFZUJZBUWHULZAUWJULZUHZUHZUU
      FUWQUWAYOUWLUXEUWQUVTYNGUUMEDUIWBUTUWQUWCUWSUWKUXDUWQUVAUWRUWBUUMEVFVNVRU
      WQUWGUXBABUWJUWHUWQUVHUXAUWFUWQUVGUWTYQUUAUUMEVGVNVQVOWDVSVSUUFYOYPUUEUHZ
      UHUXFYOYPUUEWEUXGUXEYOYPUWSUUEUXDIUWRHUWBNMWFUUEUUDAUWJULUXDUUDACUWJCHVIT
      UWJOHUWBVIMWGWHWJUUDUXCAUWJUUDUUCBUWHULUXCUUCBFUWHPWJUUCUXBBUWHYTUWFUUBUX
      AYSUWEGJUWDYQYRQWIWGKUWTYQUUARWIWFWKWLWKWLWMWNWLWOVSWAUUOUURUVOUUOUULUUMW
      PZUGUGXAZSZUURUUOGUXHUETZSZUXHUEXBZSZUXJUUOUXLUUNUXKGUULUUMUEWQWRWSGUXHUE
      WTUXNUXJUXMUXIUXHUAUBUGUGUVAUCUKZUJZUVEUDUKZTZYQYRUXQTZUVGUFZUJZBUVJULZAU
      XOVITZULZUHZUCUVBXCZUDUUSXDZUEABUCUBUDUAXEZUYFUDUUSUULUUMUIXFXGZXHWRWSXIU
      ULUUMUGUGUBXJXKXLUURUUOGUYGSUVOUURUUNUYGGUUPUUQUYGUQSUUNUYGUJUYIUAUBUGUGU
      YGUEUQUYHXMXNUTUYFUVNUDGUUSUYFUVCUYBAUVLULZUHZUXQGUJZUVNUYEUYKUCUVBUULVFX
      OUXOUVBUJZUXPUVCUYDUYJUXOUVBUVAXPUYMUYBAUYCUVLUXOUVBVIVNXQVSXRUYLUYJUVMUV
      CUYLUYAUVIABUVLUVJUYLUXRUVFUXTUVHUVEUXQGXSUYLUXSUUAYQUVGYRUXQGXSXTYAWDWCY
      BYCYEYDYFYG $.
      $( [1-Jan-2015] $)
  $}

  ${
    $d F a b $.  $d S a b $.  $d T a b $.  $d V a b $.  $d L a b $.
    $d K a b $.
    islmhm2.k $e |- K = ( Scalar ` S ) $.
    islmhm2.l $e |- L = ( Scalar ` T ) $.
    $( Non-quantified consequences of a left module homomorphism.  (Contributed
       by Stefan O'Rear, 1-Jan-2015.) $)
    islmhm2 $p |- ( ( F e. ( S LMHom T ) /\ T e. V ) ->
        ( ( S e. LMod /\ T e. LMod ) /\ ( F e. ( S GrpHom T ) /\ L = K ) ) ) $=
      ( va vb co wcel wa clmod wceq cv cvsca cfv cbs eqid clmhm cghm w3a islmhm
      wral biimpac 3simpa anim2i syl ) CABUAKLZBFLZMANLBNLMZCABUBKLZEDOZIPZJPZA
      QRZKCRUOUPCRBQRZKOJASRZUEIDSRZUEZUCZMZULUMUNMZMUKUJVCIJUTABUSCDEUQURFGHUT
      TUSTUQTURTUDUFVBVDULUMUNVAUGUHUI $.
      $( [1-Jan-2015] $)

    $( A homomorphism of left modules constrains both modules to the same ring
       of scalars.  (Contributed by Stefan O'Rear, 1-Jan-2015.) $)
    lmhmsca $p |- ( ( F e. ( S LMHom T ) /\ T e. V ) -> L = K ) $=
      ( clmhm co wcel wa clmod cghm wceq islmhm2 simprr syl ) CABIJKBFKLAMKBMKL
      ZCABNJKZEDOZLLUAABCDEFGHPSTUAQR $.
      $( [1-Jan-2015] $)
  $}

  ${
    $( A homomorphism of left modules is a homomorphism of groups.
       (Contributed by Stefan O'Rear, 1-Jan-2015.) $)
    lmghm $p |- ( F e. ( S LMHom T ) -> F e. ( S GrpHom T ) ) $=
      ( clmhm co wcel cvv cghm wa clmod csca cfv wceq eqid islmhm2 simprl wn wi
      syl eleq2d noel reldmlmhm ovprc1 mtbiri con4i mpdan ovprc2 imbi12d mpbiri
      c0 impcom pm2.61dan ) CABDEZFZBGFZCABHEZFZUNUOIAJFZBJFIZUQBKLZAKLZMZIIUQA
      BCVAUTGVANZUTNOUSUQVBPSUOQZUNUQVDUNUQRCAADEZFZCAAHEZFZRVFAGFZVHVIVFVIQZVF
      CUJFCUAVJVEUJCAADUBUCTUDUEVFVIIURURIZVHVAVAMZIIVHAACVAVAGVCVCOVKVHVLPSUFV
      DUNVFUQVHVDUMVECABDUGTVDUPVGCABHUGTUHUIUKUL $.
      $( [1-Jan-2015] $)

    $( A homomorphism of left modules has a left module as codomain.
       (Contributed by Stefan O'Rear, 1-Jan-2015.) $)
    lmhmlmod2 $p |- ( ( F e. ( S LMHom T ) /\ T e. V ) -> T e. LMod ) $=
      ( clmhm co wcel wa clmod cghm csca cfv wceq eqid islmhm2 simplr syl ) CAB
      EFGBDGHAIGZBIGZHCABJFGBKLZAKLZMHZHSABCUATDUANTNORSUBPQ $.
      $( [1-Jan-2015] $)

    $( A homomorphism of left modules has a left module as domain.
       (Contributed by Stefan O'Rear, 1-Jan-2015.) $)
    lmhmlmod1 $p |- ( F e. ( S LMHom T ) -> S e. LMod ) $=
      ( clmhm co wcel cvv clmod wa cghm csca cfv wceq islmhm2 simpll syl eleq2d
      eqid wn c0 ovprc2 reldmlmhm ovprc1 mtbiri con4i lmhmlmod2 mpdan pm2.61dan
      noel syl6bi impcom ) CABDEZFZBGFZAHFZUMUNIUOBHFZICABJEFBKLZAKLZMIZIUOABCU
      RUQGURRUQRNUOUPUSOPUNSZUMUOUTUMCAADEZFZUOUTULVACABDUAQVBAGFZUOVCVBVCSZVBC
      TFCUIVDVATCAADUBUCQUDUEAACGUFUGUJUKUH $.
      $( [1-Jan-2015] $)
  $}

  ${
    lmhmf.b $e |- B = ( Base ` S ) $.
    lmhmf.c $e |- C = ( Base ` T ) $.
    $( A homomorphism of left modules is a function.  (Contributed by Stefan
       O'Rear, 1-Jan-2015.) $)
    lmhmf $p |- ( ( F e. ( S LMHom T ) /\ T e. V ) -> F : B --> C ) $=
      ( clmhm co wcel cghm wf lmghm ghmf sylan ) ECDIJKECDLJKDFKABEMCDENCDEFABG
      HOP $.
      $( [1-Jan-2015] $)
  $}

  ${
    $d K a b $.  $d S a b $.  $d T a b $.  $d E a b $.  $d M a b $.
    $d N a b $.  $d B a b $.  $d X a b $.  $d Y a b $.  $d V a b $.
    $d F a b $.
    lmhmlin.k $e |- K = ( Scalar ` S ) $.
    lmhmlin.b $e |- B = ( Base ` K ) $.
    lmhmlin.e $e |- E = ( Base ` S ) $.
    lmhmlin.m $e |- M = ( vsca ` S ) $.
    lmhmlin.n $e |- N = ( vsca ` T ) $.
    $( A homomorphism of left modules is ` K ` -linear.  (Contributed by Stefan
       O'Rear, 1-Jan-2015.) $)
    lmhmlin $p |- ( ( ( F e. ( S LMHom T ) /\ T e. V ) /\
        ( X e. B /\ Y e. E ) ) -> ( F ` ( X M Y ) ) = ( X N ( F ` Y ) ) ) $=
      ( co wcel cfv wceq va vb clmhm wa cv wral cghm csca clmod w3a eqid islmhm
      biimpac simprd simp3d oveq1 fveq2d eqeq12d oveq2 fveq2 oveq2d rcla42v
      mpan9 ) EBCUCQRZCIRZUDZUAUEZUBUEZGQZESZVGVHESZHQZTZUBDUFUAAUFZJARKDRUDJKG
      QZESZJKESZHQZTZVFEBCUGQRZCUHSZFTZVNVFBUIRCUIRUDZVTWBVNUJZVEVDWCWDUDUAUBAB
      CDEFWAGHILWAUKMNOPULUMUNUOVMVSJVHGQZESZJVKHQZTUAUBJKADVGJTZVJWFVLWGWHVIWE
      EVGJVHGUPUQVGJVKHUPURVHKTZWFVPWGVRWIWEVOEVHKJGUSUQWIVKVQJHVHKEUTVAURVBVC
      $.
      $( [1-Jan-2015] $)
  $}

  ${
    $d F a b c $.  $d S a b c $.  $d T a b c $.  $d U a b c $.  $d X a b c $.
    $d Y a b c $.  $d V a b c $.
    lmhmima.x $e |- X = ( LSubSp ` S ) $.
    lmhmima.y $e |- Y = ( LSubSp ` T ) $.
    $( The image of a subspace under a homomorphism.  (Contributed by Stefan
       O'Rear, 1-Jan-2015.) $)
    lmhmima $p |- ( ( F e. ( S LMHom T ) /\ T e. V /\ U e. X ) ->
        ( F " U ) e. Y ) $=
      ( va vb vc co wcel cfv cbs wa wceq eqid syl clmhm w3a cima csubg cv cvsca
      wral csca cghm lmghm 3ad2ant1 clmod lmhmlmod1 simp3 lsssubg syl2anc simp2
      ghmima syl3anc wrex wb wfn wss wf lmhmf 3adant3 ffn lssss fvelimab adantr
      simpl1 simpl2 lmhmsca fveq2d eleq2d biimpa adantrr sselda adantrl lmhmlin
      syl22anc wfun cdm ffun fdm sseqtr4d jca simpl3 lssvscl funfvima2 eqeltrrd
      simprr sylc anassrs oveq2 eleq1d syl5ibcom rexlimdva ralrimivva lmhmlmod2
      sylbid impr islss4 mpbir2and ) DABUAMNZBENZCFNZUBZDCUCZGNZXIBUDOZNZJUEZKU
      EZBUFOZMZXINZKXIUGJBUHOZPOZUGZXHDABUIMNZCAUDONZXFXLXEXFYAXGABDUJUKXHAULNZ
      XGYBXEXFYCXGABDUMUKZXEXFXGUNZFCAHUOUPXEXFXGUQABCDEURUSXHXQJKXSXIXHXMXSNZX
      NXINZXQXHYFQZYGLUEZDOZXNRZLCUTZXQXHYGYLVAZYFXHDAPOZVBZCYNVCZYMXHYNBPOZDVD
      ZYOXEXFYRXGYNYQABDEYNSZYQSZVEVFZYNYQDVGTXHXGYPYEFCYNAYSHVHTZLYNCXNDVIUPVJ
      YHYKXQLCYHYICNZQXMYJXOMZXINZYKXQXHYFUUCUUEXHYFUUCQZQZXMYIAUFOZMZDOZUUDXIU
      UGXEXFXMAUHOZPOZNZYIYNNZUUJUUDRXEXFXGUUFVKXEXFXGUUFVLXHYFUUMUUCXHYFUUMXHX
      SUULXMXHXRUUKPXEXFXRUUKRXGABDUUKXREUUKSZXRSZVMVFVNVOVPVQZXHUUCUUNYFXHCYNY
      IUUBVRVSUULABYNDUUKUUHXOEXMYIUUOUULSZYSUUHSZXOSZVTWAUUGDWBZCDWCZVCZQZUUIC
      NZUUJXINXHUVDUUFXHUVAUVCXHYRUVAUUAYNYQDWDTXHCYNUVBUUBXHYRUVBYNRUUAYNYQDWE
      TWFWGVJUUGYCXGUUMUUCUVEXHYCUUFYDVJXEXFXGUUFWHUUQXHYFUUCWLUULFUUHCUUKAXMYI
      UUOUUSUURHWIWACUUIDWJWMWKWNYKUUDXPXIYJXNXMXOWOWPWQWRXAXBWSXHBULNZXJXLXTQV
      AXEXFUVFXGABDEWTVFXSGXOXIXRXKYQBJKUUPXSSYTUUTIXKSXCTXD $.
      $( [1-Jan-2015] $)

    $( The inverse image of a subspace under a homomorphism.  (Contributed by
       Stefan O'Rear, 1-Jan-2015.) $)
    lmhmpreima $p |- ( ( F e. ( S LMHom T ) /\ U e. Y ) ->
        ( `' F " U ) e. X ) $=
      ( va vb co wcel wa cfv cbs adantr cvv clss syl eqid clmhm ccnv cima csubg
      cvsca wral csca cghm lmghm clmod cdm elfvdm elex eleq2s lmhmlmod2 lsssubg
      sylan2 sylancom ghmpreima syl2anc lmhmlmod1 ad2antrr simprl cnvimass wceq
      cv wf lmhmf fdm syl5sseq sselda adantrl lmodvscl syl3anc ad2antlr lmhmlin
      simpll syl22anc simplr lmhmsca fveq2d biimpar adantrr wfun ffun fvimacnvi
      eleq2d simprr lssvscl eqeltrd wb wfn elpreima mpbir2and ralrimivva islss4
      ffn 3syl ) DABUAKLZCFLZMZDUBCUCZELZXBAUDNZLZIVFZJVFZAUENZKZXBLZJXBUFIAUGN
      ZONZUFZXADABUHKLZCBUDNLZXEWSXNWTABDUIPWSWTBUJLZXOWTWSBQLZXPXQCBRNZFCXRLBR
      UKZLXQCBRULBXSUMSHUNZABDQUOUQZFCBHUPURABDCUSUTXAXJIJXLXBXAXFXLLZXGXBLZMZM
      ZXJXIAONZLZXIDNZCLZYEAUJLZYBXGYFLZYGWSYJWTYDABDVAZVBXAYBYCVCZXAYCYKYBXAXB
      YFXGXADUKZXBYFDCVDXAYFBONZDVGZYNYFVEWTWSXQYPXTYFYOABDQYFTZYOTVHUQZYFYODVI
      SVJVKVLZXFXHXKXLYFAXGYQXKTZXHTZXLTZVMVNYEYHXFXGDNZBUENZKZCYEWSXQYBYKYHUUE
      VEWSWTYDVQWTXQWSYDXTVOYMYSXLABYFDXKXHUUDQXFXGYTUUBYQUUAUUDTZVPVRYEXPWTXFB
      UGNZONZLZUUCCLZUUECLXAXPYDYAPWSWTYDVSXAYBUUIYCXAUUIYBXAUUHXLXFXAUUGXKOWTW
      SXQUUGXKVEXTABDXKUUGQYTUUGTZVTUQWAWGWBWCYEDWDZYCUUJXAUULYDXAYPUULYRYFYODW
      ESPXAYBYCWHXGCDWFUTUUHFUUDCUUGBXFUUCUUKUUFUUHTHWIVRWJXAXJYGYIMWKZYDXAYPDY
      FWLUUMYRYFYODWQYFXICDWMWRPWNWOXAYJXCXEXMMWKWSYJWTYLPXLEXHXBXKXDYFAIJYTUUB
      YQUUAGXDTWPSWN $.
      $( [1-Jan-2015] $)
  $}

  ${
    lmhmlsp.v $e |- V = ( Base ` S ) $.
    lmhmlsp.k $e |- K = ( LSpan ` S ) $.
    lmhmlsp.l $e |- L = ( LSpan ` T ) $.
    $( Homomorphisms preserve spans. $)

    $( Homomorphisms preserve spans.  (Contributed by Stefan O'Rear,
       1-Jan-2015.) $)
    lmhmlsp $p |- ( ( F e. ( S LMHom T ) /\ T e. W /\ U C_ V ) ->
        ( F " ( K ` U ) ) = ( L ` ( F " U ) ) ) $=
      ( wcel wss cfv cima eqid 3adant3 syl clmod syl2anc clmhm co w3a wfun ccnv
      cbs wf lmhmf ffun clss lmhmlmod1 3ad2ant1 simp1 lmhmlmod2 crn imassrn frn
      syl5ss lspcl lmhmpreima cdm cin incom wceq simp3 fdm sseqtr4d df-ss sylib
      syl5req dminss a1i eqsstrd lspssid imass2 sstrd syl3anc funimass2 lmhmima
      lspssp syld3an3 eqssd ) DABUAUBLZBHLZCGMZUCZDCENZOZDCOZFNZWFDUDZWGDUEZWJO
      ZMZWHWJMWFGBUFNZDUGZWKWCWDWPWEGWOABDHIWOPZUHQZGWODUIRWFASLZWMAUJNZLZCWMMW
      NWCWDWSWEABDUKULZWFWCWJBUJNZLZXAWCWDWEUMWFBSLZWIWOMZXDWCWDXEWEABDHUNQZWFW
      IDUOZWODCUPWFWPXHWOMWRGWODUQRURZXCWIFWOBWQXCPZKUSTABWJDWTXCWTPZXJUTTWFCWL
      WIOZWMWFCDVAZCVBZXLWFXNCXMVBZCXMCVCWFCXMMXOCVDWFCGXMWCWDWEVEZWFWPXMGVDWRG
      WODVFRVGCXMVHVIVJXNXLMWFCDVKVLVMWFWIWJMZXLWMMWFXEXFXQXGXIWIFWOBWQKVNTWIWJ
      WLVORVPWTCWMEAXKJVTVQWGWJDVRTWFXEWHXCLZWIWHMZWJWHMXGWCWDWEWGWTLZXRWFWSWEX
      TXBXPWTCEGAIXKJUSTABWGDHWTXCXKXJVSWAWFCWGMZXSWFWSWEYAXBXPCEGAIJVNTCWGDVOR
      XCWIWHFBXJKVTVQWB $.
      $( [1-Jan-2015] $)
  $}

  ${
    $( The range of a homomorphism is a submodule.  (Contributed by Stefan
       O'Rear, 1-Jan-2015.) $)
    lmhmrnlss $p |- ( ( F e. ( S LMHom T ) /\ T e. V ) ->
        ran F e. ( LSubSp ` T ) ) $=
      ( clmhm co wcel wa cbs cfv cima crn clss wf wfn wceq eqid lmhmf ffn fnima
      3syl clmod lmhmlmod1 adantr lss1 syl lmhmima mpd3an3 eqeltrrd ) CABEFGZBD
      GZHZCAIJZKZCLZBMJZULUMBIJZCNCUMOUNUOPUMUQABCDUMQZUQQRUMUQCSUMCTUAUJUKUMAM
      JZGZUNUPGULAUBGZUTUJVAUKABCUCUDUSUMAURUSQZUEUFABUMCDUSUPVBUPQUGUHUI $.
      $( [1-Jan-2015] $)

    lmhmkerlss.k $e |- K = ( `' F " { Z } ) $.
    lmhmkerlss.z $e |- Z = ( 0g ` T ) $.
    lmhmkerlss.u $e |- U = ( LSubSp ` S ) $.
    $( The kernel of a homomorphism is a submodule.  (Contributed by Stefan
       O'Rear, 1-Jan-2015.) $)
    lmhmkerlss $p |- ( ( F e. ( S LMHom T ) /\ T e. V ) -> K e. U ) $=
      ( clmhm co wcel wa ccnv csn cima clss cfv clmod lmhmlmod2 eqid lsssn0 syl
      lmhmpreima syldan syl5eqel ) DABKLMZBFMZNZEDOGPZQZCHUHUIUKBRSZMZULCMUJBTM
      UNABDFUAUMBGIUMUBZUCUDABUKDCUMJUOUEUFUG $.
      $( [1-Jan-2015] $)
  $}

  ${
    $d S a b $.  $d X a b $.  $d F a b $.  $d T a b $.  $d V a b $.
    $d R a b $.  $d U a b $.
    reslmhm.u $e |- U = ( LSubSp ` S ) $.
    reslmhm.r $e |- R = ( S |`s X ) $.
    $( Restriction of a homomorphism to a subspace.  (Contributed by Stefan
       O'Rear, 1-Jan-2015.) $)
    reslmhm $p |- ( ( F e. ( S LMHom T ) /\ T e. V /\ X e. U ) ->
        ( F |` X ) e. ( R LMHom T ) ) $=
      ( va vb co wcel clmod wa cfv wceq cbs eqid syl clmhm cres cghm csca cvsca
      w3a wral lmhmlmod1 3ad2ant1 simp3 lsslmod syl2anc lmhmlmod2 3adant3 csubg
      jca lmghm lsssubg simp2 resghm syl3anc lmhmsca simpl1 simpl2 simprl lssss
      wss adantr ressbas2 eleq2d biimpar adantrl sseldd lmhmlin syl22anc simpl3
      cv lssvscl fvres oveq2d 3eqtr4d ralrimivva 3jca resssca ressvsca 3ad2ant2
      wb islmhm mpbir2and ) EBCUALMZCFMZGDMZUFZEGUBZACUALMZANMZCNMZOZWNACUCLMZC
      UDPZBUDPZQZJVQZKVQZBUEPZLZWNPZXCXDWNPZCUEPZLZQZKARPZUGJXARPZUGZUFZWMWPWQW
      MBNMZWLWPWJWKXPWLBCEUHUIZWJWKWLUJZDGBAIHUKULWJWKWQWLBCEFUMUNUPWMWSXBXNWME
      BCUCLMZGBUOPMZWKWSWJWKXSWLBCEUQUIWMXPWLXTXQXRDGBHURULWJWKWLUSBCAEFGIUTVAW
      JWKXBWLBCEXAWTFXASZWTSZVBUNWMXKJKXMXLWMXCXMMZXDXLMZOZOZXFEPZXCXDEPZXILZXG
      XJYFWJWKYCXDBRPZMYGYIQWJWKWLYEVCWJWKWLYEVDWMYCYDVEZYFGYJXDWMGYJVGZYEWMWLY
      LXRDGYJBYJSZHVFTZVHWMYDXDGMZYCWMYOYDWMGXLXDWMYLGXLQYNGYJABIYMVITVJVKVLZVM
      XMBCYJEXAXEXIFXCXDYAXMSZYMXESZXISZVNVOYFXFGMZXGYGQYFXPWLYCYOYTWMXPYEXQVHW
      JWKWLYEVPYKYPXMDXEGXABXCXDYAYRYQHVRVOXFGEVSTYFYOXJYIQYPYOXHYHXCXIXDGEVSVT
      TWAWBWCWKWJWOWRXOOWGWLJKXMACXLWNXAWTXEXIFGXABAIYAWDYBYQXLSGXEBAIYRWEYSWHW
      FWI $.
      $( [1-Jan-2015] $)
  $}

