$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Modules and vector spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LMod $.
  $c LVec $.

  $( Extend class notation with class of all left modules. $)
  clmod $a class LMod $.

  $( Extend class notation with class of all left vector spaces. $)
  clvec $a class LVec $.

  ${
    $d a f g k p q r s t v w x $.
    $( Def. 8.5.11, p. 218. $)
    $( Define the class of all left modules, which are generalizations of left
       vector spaces.  A left module over a ring is an (Abelian) group
       (vectors) together with a ring (scalars) and a left scalar product
       connecting them. $)
    df-lmod $a |- LMod = { g e. Grp | [ ( Base ` g ) / v ]
      [ ( +g ` g ) / a ] [ ( Scalar ` g ) / f ] [ ( vsca ` g ) / s ]
      [ ( Base ` f ) / k ] [ ( +g ` f ) / p ] [ ( .r ` f ) / t ]
      ( f e. Ring /\ A. q e. k A. r e. k A. x e. v A. w e. v
                ( ( ( r s w ) e. v
                 /\ ( r s ( w a x ) ) = ( ( r s w ) a ( r s x ) )
                 /\ ( ( q p r ) s w ) = ( ( q s w ) a ( r s w ) )
               ) /\ ( ( ( q t r ) s w ) = ( q s ( r s w ) )
                 /\ ( ( 1r ` f ) s w ) = w ) ) ) } $.
  $}

  ${
    $d a f g k p q r s v w x A $.  $d a f g k p q r s v w x F $.  $d r w x R $.
    $d a f g k p q r s v w x K $.  $d a f g k p q r s t v w x T $.  $d w x X $.
    $d a f g k p q r s v w x P $.  $d q r w x Q $.  $d a f g v W $.  $d w Y $.
    $d a f g k p q r s v w x S $.  $d a f g k p q r s v w x U $.
    $d a f g k p q r s v w x V $.
    islmod.v $e |- V = ( Base ` W ) $.
    islmod.a $e |- A = ( +g ` W ) $.
    islmod.s $e |- S = ( vsca ` W ) $.
    islmod.f $e |- F = ( Scalar ` W ) $.
    islmod.k $e |- K = ( Base ` F ) $.
    islmod.p $e |- P = ( +g ` F ) $.
    islmod.t $e |- T = ( .r ` F ) $.
    islmod.u $e |- U = ( 1r ` F ) $.
    $( The predicate "is a left module". $)
    islmod $p |- ( W e. LMod <->
      ( W e. Grp /\ F e. Ring /\ A. q e. K A. r e. K A. x e. V A. w e. V
                ( ( ( r S w ) e. V
                 /\ ( r S ( w A x ) ) = ( ( r S w ) A ( r S x ) )
                 /\ ( ( q P r ) S w ) = ( ( q S w ) A ( r S w ) )
               ) /\ ( ( ( q T r ) S w ) = ( q S ( r S w ) )
                 /\ ( U S w ) = w ) ) ) ) $=
      ( vf vs vv va vp vt vk vg clmod wcel cgrp crg cv co wceq w3a wral cur cfv
      wa cmulr wsbc cplusg cbs cvsca csca fveq2 syl6eqr dfsbcq syl fvex eqeltri
      wb cvv sbcbidgv mpan2 bitrd simp3 fveq2d oveq oveq1d eqeq1d anbi1d anbi2d
      2ralbidv sbcie eleq1d simp1 eleq2d simp2 oveq2d eqeq12d 3anbi123d anbi12d
      oveqd eqeq2d raleqbidv syl5bb sbc3ie oveq12d syl6bb df-lmod elrab2 3anass
      eqtrd bitri bitr4i ) KUJUKKULUKZHUMUKZLUNZBUNZEUOZJUKZXKXLAUNZCUOZEUOZXMX
      KXOEUOZCUOZUPZMUNZXKDUOZXLEUOZYAXLEUOZXMCUOZUPZUQZYAXKFUOZXLEUOZYAXMEUOZU
      PZGXLEUOZXLUPZVAZVAZBJURAJURZLIURZMIURZVAZVAXIXJYRUQUBUNZUMUKZXKXLUCUNZUO
      ZUDUNZUKZXKXLXOUEUNZUOZUUBUOZUUCXKXOUUBUOZUUFUOZUPZYAXKUFUNZUOZXLUUBUOZYA
      XLUUBUOZUUCUUFUOZUPZUQZYAXKUGUNZUOZXLUUBUOZYAUUCUUBUOZUPZYTUSUTZXLUUBUOZX
      LUPZVAZVAZBUUDURAUUDURZLUHUNZURMUVJURZVAZUGYTVBUTZVCZUFYTVDUTZVCZUHYTVEUT
      ZVCZUCUIUNZVFUTZVCZUBUVSVGUTZVCZUEUVSVDUTZVCZUDUVSVEUTZVCZYSUIKULUJUVSKUP
      ZUWGUVRUCEVCZUBHVCZUECVCZUDJVCZYSUWHUWGUWEUDJVCZUWLUWHUWFJUPUWGUWMVNUWHUW
      FKVEUTZJUVSKVEVHNVIUWEUDUWFJVJVKUWHJVOUKUWMUWLVNJUWNVONKVEVLVMZUWHUWEUWKU
      DJVOUWHUWEUWCUECVCZUWKUWHUWDCUPUWEUWPVNUWHUWDKVDUTZCUVSKVDVHOVIUWCUEUWDCV
      JVKUWHCVOUKUWPUWKVNCUWQVOOKVDVLVMZUWHUWCUWJUECVOUWHUWCUWAUBHVCZUWJUWHUWBH
      UPUWCUWSVNUWHUWBKVGUTZHUVSKVGVHQVIUWAUBUWBHVJVKUWHHVOUKUWSUWJVNHUWTVOQKVG
      VLVMZUWHUWAUWIUBHVOUWHUVTEUPUWAUWIVNUWHUVTKVFUTZEUVSKVFVHPVIUVRUCUVTEVJVK
      VPVQVRVPVQVRVPVQVRUWLXJUUCJUKZXKXPUUBUOZUUCUUICUOZUPZUUNUUOUUCCUOZUPZUQZY
      HXLUUBUOZUVBUPZGXLUUBUOZXLUPZVAZVAZBJURZAJURZLUVJURZMUVJURZVAZUFDVCZUHIVC
      ZUCEVCZYSUWIUYCUDUEUBJCHUWOUWRUXAUUDJUPZUUFCUPZYTHUPZUQZEVOUKUWIUYCVNEUXB
      VOPKVFVLVMZUYGUVRUYBUCEVOUYGUVRUVPUHIVCZUYBUYGUVQIUPUVRUYIVNUYGUVQHVEUTZI
      UYGYTHVEUYDUYEUYFVSZVTRVIUVPUHUVQIVJVKUYGIVOUKUYIUYBVNIUYJVORHVEVLVMZUYGU
      VPUYAUHIVOUYGUVPUVNUFDVCZUYAUYGUVODUPUVPUYMVNUYGUVOHVDUTZDUYGYTHVDUYKVTSV
      IUVNUFUVODVJVKUYGDVOUKUYMUYAVNDUYNVOSHVDVLVMZUYGUVNUXTUFDVOUYGUVNUVLUGFVC
      ZUXTUYGUVMFUPUVNUYPVNUYGUVMHVBUTZFUYGYTHVBUYKVTTVIUVLUGUVMFVJVKUYPUUAUURU
      XKUVFVAZVAZBUUDURZAUUDURZLUVJURMUVJURZVAZUYGUXTUVLVUCUGFFUYQVOTHVBVLVMUUS
      FUPZUVKVUBUUAVUDUVIVUAMLUVJUVJVUDUVHUYSABUUDUUDVUDUVGUYRUURVUDUVCUXKUVFVU
      DUVAUXJUVBVUDUUTYHXLUUBYAXKUUSFWAWBWCWDWEWFWFWEWGUYGUUAXJVUBUXSUYGYTHUMUY
      KWHUYGVUAUXQMLUVJUVJUYGUYTUXPAUUDJUYDUYEUYFWIZUYGUYSUXOBUUDJVUEUYGUURUXIU
      YRUXNUYGUUEUXCUUKUXFUUQUXHUYGUUDJUUCVUEWJUYGUUHUXDUUJUXEUYGUUGXPXKUUBUYGU
      UFCXLXOUYDUYEUYFWKZWPWLUYGUUFCUUCUUIVUFWPWMUYGUUPUXGUUNUYGUUFCUUOUUCVUFWP
      WQWNUYGUVFUXMUXKUYGUVEUXLXLUYGUVDGXLUUBUYGUVDHUSUTGUYGYTHUSUYKVTUAVIWBWCW
      EWOWRWRWFWOWSVRVPVQVRVPVQVRVPVQWTUXTYSUCUHUFEIDUYHUYLUYOUUBEUPZUVJIUPZUUL
      DUPZUQZUXSYRXJVUJUXRYQMUVJIVUGVUHVUIWKZVUJUXQYPLUVJIVUKVUJUXOYOABJJVUJUXI
      YGUXNYNVUJUXCXNUXFXTUXHYFVUJUUCXMJVUJUUBEXKXLVUGVUHVUIWIZWPZWHVUJUXDXQUXE
      XSVUJUUBEXKXPVULWPVUJUUCXMUUIXRCVUMVUJUUBEXKXOVULWPXAWMVUJUUNYCUXGYEVUJUU
      NYBXLUUBUOYCVUJUUMYBXLUUBVUJUULDYAXKVUGVUHVUIVSWPWBVUJUUBEYBXLVULWPXFVUJU
      UOYDUUCXMCVUJUUBEYAXLVULWPVUMXAWMWNVUJUXKYKUXMYMVUJUXJYIUVBYJVUJUUBEYHXLV
      ULWPVUJUVBYAXMUUBUOYJVUJUUCXMYAUUBVUMWLVUJUUBEYAXMVULWPXFWMVUJUXLYLXLVUJU
      UBEGXLVULWPWCWOWOWFWRWRWEWTXGXBABUDUGUBUIUHUCLMUFUEXCXDXIXJYRXEXH $.
      $( [19-Jun-2014] $) $( [4-Nov-2013] $)

    $( Lemma for properties of a left module. $)
    lmodlema $p |- ( ( W e. LMod /\ ( Q e. K /\ R e. K )
          /\ ( X e. V /\ Y e. V ) ) ->
                ( ( ( R S Y ) e. V
                 /\ ( R S ( Y A X ) ) = ( ( R S Y ) A ( R S X ) )
                 /\ ( ( Q P R ) S Y ) = ( ( Q S Y ) A ( R S Y ) )
               ) /\ ( ( ( Q T R ) S Y ) = ( Q S ( R S Y ) )
                 /\ ( U S Y ) = Y ) ) ) $=
      ( vw vx vr vq clmod wcel wa co wceq w3a cv wral cgrp islmod simp3bi oveq1
      crg oveq1d eqeq12d 3anbi3d anbi1d anbi12d 2ralbidv eleq1d oveq2 3anbi123d
      oveq12d oveq2d rcla42v mpan9 3anbi2d id syl5com 3impia ) KUFUGZCIUGDIUGUH
      ZLJUGMJUGUHZDMEUIZJUGZDMLAUIZEUIZVSDLEUIZAUIZUJZCDBUIZMEUIZCMEUIZVSAUIZUJ
      ZUKZCDFUIZMEUIZCVSEUIZUJZGMEUIZMUJZUHZUHZVPVQUHDUBULZEUIZJUGZDWTUCULZAUIZ
      EUIZXADXCEUIZAUIZUJZWFWTEUIZCWTEUIZXAAUIZUJZUKZWLWTEUIZCXAEUIZUJZGWTEUIZW
      TUJZUHZUHZUBJUMUCJUMZVRWSVPUDULZWTEUIZJUGZYBXDEUIZYCYBXCEUIZAUIZUJZUEULZY
      BBUIZWTEUIZYIWTEUIZYCAUIZUJZUKZYIYBFUIZWTEUIZYIYCEUIZUJZXRUHZUHZUBJUMUCJU
      MZUDIUMUEIUMZVQYAVPKUNUGHURUGUUCUCUBABEFGHIJKUDUENOPQRSTUAUOUPUUBYAYDYHCY
      BBUIZWTEUIZXJYCAUIZUJZUKZCYBFUIZWTEUIZCYCEUIZUJZXRUHZUHZUBJUMUCJUMUEUDCDI
      IYICUJZUUAUUNUCUBJJUUOYOUUHYTUUMUUOYNUUGYDYHUUOYKUUEYMUUFUUOYJUUDWTEYICYB
      BUQUSUUOYLXJYCAYICWTEUQUSUTVAUUOYSUULXRUUOYQUUJYRUUKUUOYPUUIWTEYICYBFUQUS
      YICYCEUQUTVBVCVDYBDUJZUUNXTUCUBJJUUPUUHXMUUMXSUUPYDXBYHXHUUGXLUUPYCXAJYBD
      WTEUQZVEUUPYEXEYGXGYBDXDEUQUUPYCXAYFXFAUUQYBDXCEUQVHUTUUPUUEXIUUFXKUUPUUD
      WFWTEYBDCBVFUSUUPYCXAXJAUUQVIUTVGUUPUULXPXRUUPUUJXNUUKXOUUPUUIWLWTEYBDCFV
      FUSUUPYCXACEUUQVIUTVBVCVDVJVKXTWSXBDWTLAUIZEUIZXAWCAUIZUJZXLUKZXSUHUCUBLM
      JJXCLUJZXMUVBXSUVCXHUVAXBXLUVCXEUUSXGUUTUVCXDUURDEXCLWTAVFVIUVCXFWCXAAXCL
      DEVFVIUTVLVBWTMUJZUVBWKXSWRUVDXBVTUVAWEXLWJUVDXAVSJWTMDEVFZVEUVDUUSWBUUTW
      DUVDUURWADEWTMLAUQVIUVDXAVSWCAUVEUSUTUVDXIWGXKWIWTMWFEVFUVDXJWHXAVSAWTMCE
      VFUVEVHUTVGUVDXPWOXRWQUVDXNWMXOWNWTMWLEVFUVDXAVSCEUVEVIUTUVDXQWPWTMWTMGEV
      FUVDVMUTVCVCVJVNVO $.
      $( [19-Jun-2014] $) $( [8-Dec-2013] $)
  $}

  ${
    $d x y z A $.  $d r u w x y z B $.  $d y z P $.  $d x y z S $.  $d y z T $.
    $d r u w x y z ph $.  $d x U $.  $d u w x y z V $.  $d r u w x W $.
    islmodd.v $e |- ( ph -> V = ( Base ` W ) ) $.
    islmodd.a $e |- ( ph -> A = ( +g ` W ) ) $.
    islmodd.f $e |- ( ph -> F = ( Scalar ` W ) ) $.
    islmodd.s $e |- ( ph -> S = ( vsca ` W ) ) $.
    islmodd.b $e |- ( ph -> B = ( Base ` F ) ) $.
    islmodd.p $e |- ( ph -> P = ( +g ` F ) ) $.
    islmodd.t $e |- ( ph -> T = ( .r ` F ) ) $.
    islmodd.u $e |- ( ph -> U = ( 1r ` F ) ) $.
    islmodd.r $e |- ( ph -> F e. Ring ) $.
    $( Group operations $)
    islmodd.l $e |- ( ph -> W e. Grp ) $.
    $( Scalar product operation $)
    islmodd.w $e |- ( ( ph /\ x e. B /\ y e. V ) -> ( x S y ) e. V ) $.
    islmodd.c $e |- ( ( ph /\ ( x e. B /\ y e. V /\ z e. V ) )
        -> ( x S ( y A z ) ) = ( ( x S y ) A ( x S z ) ) ) $.
    islmodd.d $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. V ) )
        -> ( ( x P y ) S z ) = ( ( x S z ) A ( y S z ) ) ) $.
    islmodd.e $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. V ) )
        -> ( ( x T y ) S z ) = ( x S ( y S z ) ) ) $.
    islmodd.g $e |- ( ( ph /\ x e. V ) -> ( U S x ) = x ) $.
    $( Properties that determine a left module.  (Contributed by Mario
       Carneiro, 22-Jun-2014.) $)
    islmodd $p |- ( ph -> W e. LMod ) $=
      ( vr vw vu cgrp wcel csca cfv crg cv cvsca co cbs cplusg w3a cmulr cur wa
      wceq wral clmod eqeltrrd 3expb ralrimivva wi oveq1 oveq2 rcla42v ad2ant2l
      eleq1d mpan9 ralrimivvva oveq12d eqeq12d oveq2d oveq1d 3com23 3exp2 imp43
      rcla43v adantll simpll sylan2 simprlr simprrr syl2anc ralrimiva id rcla4v
      mpd 3jca ad2antll jca32 anassrs fveq2d eqtrd oveqd eleq12d eqidd oveq123d
      3anbi123d eqeq1d anbi12d raleqbidv mpbid eqid islmod syl3anbrc ) AMULUMMU
      NUOZUPUMUIUQZUJUQZMURUOZUSZMUTUOZUMZXQXRUKUQZMVAUOZUSZXSUSZXTXQYCXSUSZYDU
      SZVFZBUQZXQXPVAUOZUSZXRXSUSZYJXRXSUSZXTYDUSZVFZVBZYJXQXPVCUOZUSZXRXSUSZYJ
      XTXSUSZVFZXPVDUOZXRXSUSZXRVFZVEZVEZUJYAVGZUKYAVGZUIXPUTUOZVGZBUUJVGZMVHUM
      UCAKXPUPPUBVIAXQXRHUSZLUMZXQXRYCEUSZHUSZUUMXQYCHUSZEUSZVFZYJXQGUSZXRHUSZY
      JXRHUSZUUMEUSZVFZVBZYJXQIUSZXRHUSZYJUUMHUSZVFZJXRHUSZXRVFZVEZVEZUJLVGZUKL
      VGZUIFVGZBFVGUULAUVOBUIFFAYJFUMZXQFUMZVEZVEUVMUKUJLLAUVSYCLUMZXRLUMZVEZUV
      MAUVSUWBVEZVEZUVEUVIUVKUWDUUNUUSUVDAYJCUQZHUSZLUMZCLVGBFVGZUWCUUNAUWGBCFL
      AUVQUWELUMUWGUDVJVKUVRUWAUWHUUNVLUVQUVTUWGUUNXQUWEHUSZLUMBCXQXRFLYJXQVFZU
      WFUWILYJXQUWEHVMZVQUWEXRVFZUWIUUMLUWEXRXQHVNZVQVOVPVRAYJUWEDUQZEUSZHUSZUW
      FYJUWNHUSZEUSZVFZDLVGCLVGBFVGZUWCUUSAUWSBCDFLLUEVSUVRUWBUWTUUSVLZUVQUVRUV
      TUWAUXAUVRUWAUVTUXAUWSUUSXQUWOHUSZUWIXQUWNHUSZEUSZVFXQXRUWNEUSZHUSZUUMUXC
      EUSZVFBCDXQXRYCFLLUWJUWPUXBUWRUXDYJXQUWOHVMUWJUWFUWIUWQUXCEUWKYJXQUWNHVMV
      TWAUWLUXBUXFUXDUXGUWLUWOUXEXQHUWEXRUWNEVMWBUWLUWIUUMUXCEUWMWCWAUWNYCVFZUX
      FUUPUXGUURUXHUXEUUOXQHUWNYCXREVNWBUXHUXCUUQUUMEUWNYCXQHVNWBWAWGWDVJWHVRUW
      DYJUWEGUSZUWNHUSZUWQUWEUWNHUSZEUSZVFZDLVGCFVGZUVDUWCAUVQUXNUVQUVRUWBWIZAU
      VQVEZUXMCDFLAUVQUWEFUMZUWNLUMZUXMAUVQUXQUXRUXMUFWEWFVKWJUWDUVRUWAUXNUVDVL
      AUVQUVRUWBWKZAUVSUVTUWAWLZUXMUVDUUTUWNHUSZUWQUXCEUSZVFCDXQXRFLUWEXQVFZUXJ
      UYAUXLUYBUYCUXIUUTUWNHUWEXQYJGVNWCUYCUXKUXCUWQEUWEXQUWNHVMZWBWAUWNXRVFZUY
      AUVAUYBUVCUWNXRUUTHVNUYEUWQUVBUXCUUMEUWNXRYJHVNUWNXRXQHVNZVTWAVOWMWQWRUWD
      YJUWEIUSZUWNHUSZYJUXKHUSZVFZDLVGCFVGZUVIUWCAUVQUYKUXOUXPUYJCDFLAUVQUXQUXR
      UYJAUVQUXQUXRUYJUGWEWFVKWJUWDUVRUWAUYKUVIVLUXSUXTUYJUVIUVFUWNHUSZYJUXCHUS
      ZVFCDXQXRFLUYCUYHUYLUYIUYMUYCUYGUVFUWNHUWEXQYJIVNWCUYCUXKUXCYJHUYDWBWAUYE
      UYLUVGUYMUVHUWNXRUVFHVNUYEUXCUUMYJHUYFWBWAVOWMWQAJYJHUSZYJVFZBLVGZUWCUVKA
      UYOBLUHWNUWAUYPUVKVLUVSUVTUYOUVKBXRLYJXRVFZUYNUVJYJXRYJXRJHVNUYQWOWAWPWSV
      RWTXAVKVKAUVPUUKBFUUJAFKUTUOUUJRAKXPUTPXBXCZAUVOUUIUIFUUJUYRAUVNUUHUKLYAN
      AUVMUUGUJLYANAUVEYQUVLUUFAUUNYBUUSYIUVDYPAUUMXTLYAAHXSXQXRQXDZNXEAUUPYFUU
      RYHAXQXQUUOYEHXSQAXQXFAEYDXRYCOXDXGAUUMXTUUQYGEYDOUYSAHXSXQYCQXDXGWAAUVAY
      MUVCYOAUUTYLXRXRHXSQAGYKYJXQAGKVAUOYKSAKXPVAPXBXCXDAXRXFZXGAUVBYNUUMXTEYD
      OAHXSYJXRQXDUYSXGWAXHAUVIUUBUVKUUEAUVGYTUVHUUAAUVFYSXRXRHXSQAIYRYJXQAIKVC
      UOYRTAKXPVCPXBXCXDUYTXGAYJYJUUMXTHXSQAYJXFUYSXGWAAUVJUUDXRAJUUCXRXRHXSQAJ
      KVDUOUUCUAAKXPVDPXBXCUYTXGXIXJXJXKXKXKXKXLUKUJYDYKXSYRUUCXPUUJYAMUIBYAXMY
      DXMXSXMXPXMUUJXMYKXMYRXMUUCXMXNXO $.
      $( [22-Jun-2014] $)
  $}

  ${
    $d q r w x F $.  $d q r w x W $.
    $( A left module is a group. $)
    lmodgrp $p |- ( W e. LMod -> W e. Grp ) $=
      ( vr vw vx vq clmod wcel cgrp csca cfv crg cv cvsca co cbs cplusg wceq wa
      wral eqid w3a cmulr cur islmod simp1bi ) AFGAHGAIJZKGBLZCLZAMJZNZAOJZGUGU
      HDLZAPJZNUINUJUGULUINUMNQELZUGUFPJZNUHUINUNUHUINUJUMNQUAUNUGUFUBJZNUHUINU
      NUJUINQUFUCJZUHUINUHQRRCUKSDUKSBUFOJZSEURSDCUMUOUIUPUQUFURUKABEUKTUMTUITU
      FTURTUOTUPTUQTUDUE $.
      $( [25-Jun-2014] $) $( [8-Dec-2013] $)

    lmodrng.1 $e |- F = ( Scalar ` W ) $.
    $( The scalar component of a left module is a ring. $)
    lmodrng $p |- ( W e. LMod -> F e. Ring ) $=
      ( vr vw vx vq clmod wcel cgrp crg cv cfv co cbs cplusg wceq wa wral eqid
      cvsca w3a cmulr cur islmod simp2bi ) BHIBJIAKIDLZELZBUAMZNZBOMZIUGUHFLZBP
      MZNUINUJUGULUINUMNQGLZUGAPMZNUHUINUNUHUINUJUMNQUBUNUGAUCMZNUHUINUNUJUINQA
      UDMZUHUINUHQRREUKSFUKSDAOMZSGURSFEUMUOUIUPUQAURUKBDGUKTUMTUITCURTUOTUPTUQ
      TUEUF $.
      $( [19-Jun-2014] $) $( [8-Dec-2013] $)

    $( The scalar component of a left module is an additive group. $)
    lmodfgrp $p |- ( W e. LMod -> F e. Grp ) $=
      ( clmod wcel crg cgrp lmodrng rnggrp syl ) BDEAFEAGEABCHAIJ $.
      $( [19-Jun-2014] $) $( [8-Dec-2013] $)
  $}

  ${
    lmodbn0.b $e |- B = ( Base ` W ) $.
    $( The base set of a left module is nonempty. $)
    lmodbn0 $p |- ( W e. LMod -> B =/= (/) ) $=
      ( clmod wcel cgrp c0 wne lmodgrp grpbn0 syl ) BDEBFEAGHBIABCJK $.
      $( [19-Jun-2014] $) $( [8-Dec-2013] $)
  $}

  ${
    lmodacl.f $e |- F = ( Scalar ` W ) $.
    lmodacl.k $e |- K = ( Base ` F ) $.
    lmodacl.p $e |- P = ( +g ` F ) $.
    $( Closure of ring addition for a left module. $)
    lmodacl $p |- ( ( W e. LMod /\ X e. K /\ Y e. K ) -> ( X P Y ) e. K ) $=
      ( clmod wcel cgrp co lmodfgrp grpcl syl3an1 ) DJKBLKECKFCKEFAMCKBDGNCABEF
      HIOP $.
      $( [19-Jun-2014] $) $( [14-Jan-2014] $)
  $}

  ${
    lmodmcl.f $e |- F = ( Scalar ` W ) $.
    lmodmcl.k $e |- K = ( Base ` F ) $.
    lmodmcl.t $e |- T = ( .r ` F ) $.
    $( Closure of ring multiplication for a left module. $)
    lmodmcl $p |- ( ( W e. LMod /\ X e. K /\ Y e. K ) -> ( X T Y ) e. K ) $=
      ( clmod wcel crg co lmodrng rngcl syl3an1 ) DJKBLKECKFCKEFAMCKBDGNCBAEFHI
      OP $.
      $( [19-Jun-2014] $) $( [14-Jan-2014] $)
  $}

  ${
    lmodsn0.f $e |- F = ( Scalar ` W ) $.
    lmodsn0.b $e |- B = ( Base ` F ) $.
    $( The set of scalars in a left module is nonempty. $)
    lmodsn0 $p |- ( W e. LMod -> B =/= (/) ) $=
      ( clmod wcel cgrp c0 wne lmodfgrp grpbn0 syl ) CFGBHGAIJBCDKABELM $.
      $( [19-Jun-2014] $) $( [8-Dec-2013] $)
  $}

  ${
    lmodvacl.v $e |- V = ( Base ` W ) $.
    lmodvacl.a $e |- A = ( +g ` W ) $.
    $( Closure of vector addition for a left module. $)
    lmodvacl $p |- ( ( W e. LMod /\ X e. V /\ Y e. V ) -> ( X A Y ) e. V ) $=
      ( clmod wcel cgrp co lmodgrp grpcl syl3an1 ) CHICJIDBIEBIDEAKBICLBACDEFGM
      N $.
      $( [19-Jun-2014] $) $( [8-Dec-2013] $)

    $( Left module vector sum is associative. $)
    lmodass $p |- ( ( W e. LMod /\ ( X e. V /\ Y e. V /\ Z e. V ) ) ->
                    ( ( X A Y ) A Z ) = ( X A ( Y A Z ) ) ) $=
      ( clmod wcel cgrp w3a co wceq lmodgrp grpass sylan ) CIJCKJDBJEBJFBJLDEAM
      FAMDEFAMAMNCOBACDEFGHPQ $.
      $( [19-Jun-2014] $) $( [10-Jan-2014] $)

    $( Left cancellation law for vector sum. $)
    lmodlcan $p |- ( ( W e. LMod /\ ( X e. V /\ Y e. V /\ Z e. V ) ) ->
                     ( ( Z A X ) = ( Z A Y ) <-> X = Y ) ) $=
      ( clmod wcel cgrp w3a co wceq wb lmodgrp grplcan sylan ) CIJCKJDBJEBJFBJL
      FDAMFEAMNDENOCPBACDEFGHQR $.
      $( [19-Jun-2014] $) $( [12-Jan-2014] $)
  $}

  ${
    lmodvscl.v $e |- V = ( Base ` W ) $.
    lmodvscl.f $e |- F = ( Scalar ` W ) $.
    lmodvscl.s $e |- S = ( vsca ` W ) $.
    lmodvscl.k $e |- K = ( Base ` F ) $.
    $( Closure of scalar product for a left module.  ( ~ hvmulcl analog.) $)
    lmodvscl $p |- ( ( W e. LMod /\ R e. K /\ X e. V ) -> ( R S X ) e. V ) $=
      ( wcel wa co pm4.24 w3a cplusg cfv wceq eqid clmod biid cur simpld simp1d
      cmulr lmodlema syl3anb ) FUALZUIADLZUJUJMZGELZULULMZAGBNZELZUIUBUJOULOUIU
      KUMPZUOAGGFQRZNBNUNUNUQNZSZAACQRZNGBNURSZUPUOUSVAPAACUFRZNGBNAUNBNSCUCRZG
      BNGSMUQUTAABVBVCCDEFGGHUQTJIKUTTVBTVCTUGUDUEUH $.
      $( [19-Jun-2014] $) $( [8-Dec-2013] $)
  $}

  ${
    lmodvsdi1.v $e |- V = ( Base ` W ) $.
    lmodvsdi1.a $e |- A = ( +g ` W ) $.
    lmodvsdi1.f $e |- F = ( Scalar ` W ) $.
    lmodvsdi1.s $e |- S = ( vsca ` W ) $.
    lmodvsdi1.k $e |- K = ( Base ` F ) $.
    $( Distributive law for scalar product.  ( ~ ax-hvdistr1 analog.) $)
    lmodvsdi1 $p |- ( ( W e. LMod /\ R e. K /\ ( X e. V /\ Y e. V ) ) ->
                      ( R S ( X A Y ) ) = ( ( R S X ) A ( R S Y ) ) ) $=
      ( wcel wa co wceq cfv eqid clmod biid pm4.24 ancom w3a cmulr cur lmodlema
      cplusg simpld simp2d syl3anb ) GUAOZUMBEOZUNUNPZHFOZIFOZPUQUPPZBHIAQCQBHC
      QZBICQAQRZUMUBUNUCUPUQUDUMUOURUEZUSFOZUTBBDUISZQHCQUSUSAQRZVAVBUTVDUEBBDU
      FSZQHCQBUSCQRDUGSZHCQHRPAVCBBCVEVFDEFGIHJKMLNVCTVETVFTUHUJUKUL $.
      $( [19-Jun-2014] $) $( [10-Jan-2014] $)
  $}

  ${
    lmodvsdi2.v $e |- V = ( Base ` W ) $.
    lmodvsdi2.a $e |- A = ( +g ` W ) $.
    lmodvsdi2.f $e |- F = ( Scalar ` W ) $.
    lmodvsdi2.s $e |- S = ( vsca ` W ) $.
    lmodvsdi2.k $e |- K = ( Base ` F ) $.
    lmodvsdi2.p $e |- P = ( +g ` F ) $.
    $( Distributive law for scalar product.  ( ~ ax-hvdistr2 analog.) $)
    lmodvsdi2 $p |- ( ( W e. LMod /\ ( Q e. K /\ R e. K ) /\ X e. V ) ->
                      ( ( Q P R ) S X ) = ( ( Q S X ) A ( R S X ) ) ) $=
      ( wcel wa co wceq clmod pm4.24 w3a cmulr cfv cur lmodlema simpld syl3an3b
      eqid simp3d ) JHQZIUAQZCGQDGQRZULULRZCDBSJESCJESDJESZASTZULUBUMUNUOUCZUPH
      QZDJJASESUPUPASTZUQURUSUTUQUCCDFUDUEZSJESCUPESTFUFUEZJESJTRABCDEVAVBFGHIJ
      JKLNMOPVAUJVBUJUGUHUKUI $.
      $( [19-Jun-2014] $) $( [10-Jan-2014] $)
  $}

  ${
    lmodvsass.v $e |- V = ( Base ` W ) $.
    lmodvsass.f $e |- F = ( Scalar ` W ) $.
    lmodvsass.s $e |- S = ( vsca ` W ) $.
    lmodvsass.k $e |- K = ( Base ` F ) $.
    lmodvsass.t $e |- T = ( .r ` F ) $.
    $( Associative law for scalar product.  ( ~ ax-hvmulass analog.) $)
    lmodvsass $p |- ( ( W e. LMod /\ ( Q e. K /\ R e. K ) /\ X e. V ) ->
                      ( ( Q T R ) S X ) = ( Q S ( R S X ) ) ) $=
      ( wcel wa co wceq cfv eqid clmod pm4.24 w3a cplusg lmodlema simprd simpld
      cur syl3an3b ) IGOZHUAOZAFOBFOPZUJUJPZABDQICQABICQZCQRZUJUBUKULUMUCZUOEUH
      SZICQIRZUPUNGOBIIHUDSZQCQUNUNUSQRABEUDSZQICQAICQUNUSQRUCUOURPUSUTABCDUQEF
      GHIIJUSTLKMUTTNUQTUEUFUGUI $.
      $( [19-Jun-2014] $) $( [10-Jan-2014] $)
  $}

  ${
    lmod0cl.f $e |- F = ( Scalar ` W ) $.
    lmod0cl.k $e |- K = ( Base ` F ) $.
    lmod0cl.z $e |- Z = ( 0g ` F ) $.
    $( The ring zero in a left module belongs to the ring base set. $)
    lmod0cl $p |- ( W e. LMod -> Z e. K ) $=
      ( clmod wcel crg lmodrng rng0cl syl ) CHIAJIDBIACEKBADFGLM $.
      $( [19-Jun-2014] $) $( [11-Jan-2014] $)
  $}

  ${
    lmod1cl.f $e |- F = ( Scalar ` W ) $.
    lmod1cl.k $e |- K = ( Base ` F ) $.
    lmod1cl.u $e |- U = ( 1r ` F ) $.
    $( The ring unit in a left module belongs to the ring base set. $)
    lmod1cl $p |- ( W e. LMod -> U e. K ) $=
      ( clmod wcel crg lmodrng rngidcl syl ) DHIBJIACIBDEKCBAFGLM $.
      $( [19-Jun-2014] $) $( [11-Jan-2014] $)
  $}

  ${
    lmodvs1.v $e |- V = ( Base ` W ) $.
    lmodvs1.f $e |- F = ( Scalar ` W ) $.
    lmodvs1.s $e |- S = ( vsca ` W ) $.
    lmodvs1.u $e |- U = ( 1r ` F ) $.
    $( Scalar product with ring unit.  ( ~ ax-hvmulid analog.) $)
    lmodvs1 $p |- ( ( W e. LMod /\ X e. V ) -> ( U S X ) = X ) $=
      ( clmod wcel wa cfv co wceq eqid w3a cplusg simprd cbs simpl adantr simpr
      lmod1cl cmulr lmodlema syl122anc ) EKLZFDLZMUIBCUANZLZULUJUJBFAOZFPZUIUJU
      BUIULUJBCUKEHUKQZJUEUCZUPUIUJUDZUQUIULULMUJUJMRZBBCUFNZOFAOBUMAOPZUNURUMD
      LBFFESNZOAOUMUMVAOZPBBCSNZOFAOVBPRUTUNMVAVCBBAUSBCUKDEFFGVAQIHUOVCQUSQJUG
      TTUH $.
      $( [19-Jun-2014] $) $( [10-Jan-2014] $)
  $}

  ${
    0vcl.v $e |- V = ( Base ` W ) $.
    0vcl.z $e |- Z = ( 0g ` W ) $.
    $( The zero vector is a vector.  ( ~ ax-hv0cl analog.) $)
    0vcl $p |- ( W e. LMod -> Z e. V ) $=
      ( clmod wcel cgrp lmodgrp grpidcl syl ) BFGBHGCAGBIABCDEJK $.
      $( [19-Jun-2014] $) $( [10-Jan-2014] $)
  $}

  ${
    0vlid.v $e |- V = ( Base ` W ) $.
    0vlid.a $e |- A = ( +g ` W ) $.
    0vlid.z $e |- Z = ( 0g ` W ) $.
    $( Left identity law for the zero vector.  ( ~ hvaddid2 analog.) $)
    0vlid $p |- ( ( W e. LMod /\ X e. V ) -> ( Z A X ) = X ) $=
      ( clmod wcel cgrp co wceq lmodgrp grplid sylan ) CIJCKJDBJEDALDMCNBACEDFG
      HOP $.
      $( [19-Jun-2014] $) $( [10-Jan-2014] $)

    $( Right identity law for the zero vector.  ( ~ ax-hvaddid analog.) $)
    0vrid $p |- ( ( W e. LMod /\ X e. V ) -> ( X A Z ) = X ) $=
      ( clmod wcel cgrp co wceq lmodgrp grprid sylan ) CIJCKJDBJDEALDMCNBACEDFG
      HOP $.
      $( [19-Jun-2014] $) $( [10-Jan-2014] $)

    $( Identity equivalent to the value of the zero vector.  Provides a
       convenient way to compute the value. $)
    0vid $p |- ( ( W e. LMod /\ X e. V ) -> ( ( X A X ) = X <-> Z = X ) ) $=
      ( clmod wcel cgrp co wceq wb lmodgrp grpid sylan ) CIJCKJDBJDDALDMEDMNCOB
      ACEDFGHPQ $.
      $( [19-Jun-2014] $) $( [9-Mar-2014] $)
  $}

  ${
    lmod0vs.v $e |- V = ( Base ` W ) $.
    lmod0vs.f $e |- F = ( Scalar ` W ) $.
    lmod0vs.s $e |- S = ( vsca ` W ) $.
    lmod0vs.o $e |- O = ( 0g ` F ) $.
    lmod0vs.z $e |- Z = ( 0g ` W ) $.
    $( Zero times a vector is the zero vector.  Equation 1a of [Kreyszig]
       p. 51.  ( ~ ax-hvmul0 analog.) $)
    lmod0vs $p |- ( ( W e. LMod /\ X e. V ) -> ( O S X ) = Z ) $=
      ( clmod wcel co cplusg cfv wceq eqid syl wa cbs crg lmodrng adantr rng0cl
      simpl simpr lmodvsdi2 syl121anc cgrp rnggrp grplid oveq1d eqtr3d lmodvscl
      syl2anc wb syl3anc 0vid syldan mpbid eqcomd ) EMNZFDNZUAZGCFAOZVFVGVGEPQZ
      OZVGRZGVGRZVFCCBPQZOZFAOZVIVGVFVDCBUBQZNZVPVEVNVIRVDVEUGZVFBUCNZVPVDVRVEB
      EIUDUEZVOBCVOSZKUFTZWAVDVEUHZVHVLCCABVODEFHVHSZIJVTVLSZUIUJVFVMCFAVFBUKNZ
      VPVMCRVFVRWEVSBULTWAVOVLBCCVTWDKUMUQUNUOVDVEVGDNZVJVKURVFVDVPVEWFVQWAWBCA
      BVODEFHIJVTUPUSVHDEVGGHWCLUTVAVBVC $.
      $( [19-Jun-2014] $) $( [12-Jan-2014] $)
  $}

  ${
    lmodvs0.f $e |- F = ( Scalar ` W ) $.
    lmodvs0.s $e |- S = ( vsca ` W ) $.
    lmodvs0.k $e |- K = ( Base ` F ) $.
    lmodvs0.z $e |- Z = ( 0g ` W ) $.
    $( Anything times the zero vector is the zero vector.  Equation 1b of
       [Kreyszig] p. 51.  ( ~ hvmul0 analog.) $)
    lmodvs0 $p |- ( ( W e. LMod /\ X e. K ) -> ( X S Z ) = Z ) $=
      ( clmod wcel wa c0g cfv cmulr co wceq eqid adantr crg lmodrng rngrz sylan
      oveq1d cbs simpl simpr rng0cl syl 0vcl lmodvsass syl121anc lmod0vs syldan
      oveq2d eqtrd 3eqtr3d ) DKLZECLZMZEBNOZBPOZQZFAQZVBFAQZEFAQZFVAVDVBFAUSBUA
      LZUTVDVBRBDGUBZCBVCEVBIVCSZVBSZUCUDUEVAVEEVFAQZVGVAUSUTVBCLZFDUFOZLZVEVLR
      USUTUGUSUTUHVAVHVMUSVHUTVITCBVBIVKUIUJUSVOUTVNDFVNSZJUKTZEVBAVCBCVNDFVPGH
      IVJULUMVAVFFEAUSUTVOVFFRVQABVBVNDFFVPGHVKJUNUOZUPUQVRUR $.
      $( [19-Jun-2014] $) $( [12-Jan-2014] $)
  $}

  ${
    lmodvnegcl.v $e |- V = ( Base ` W ) $.
    lmodvnegcl.n $e |- N = ( invg ` W ) $.
    $( Closure of vector negative. $)
    lmodvnegcl $p |- ( ( W e. LMod /\ X e. V ) -> ( N ` X ) e. V ) $=
      ( clmod wcel cgrp cfv lmodgrp grpinvcl sylan ) CGHCIHDBHDAJBHCKBCADEFLM
      $.
      $( [19-Jun-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    lmodvnegid.v $e |- V = ( Base ` W ) $.
    lmodvnegid.p $e |- P = ( +g ` W ) $.
    lmodvnegid.z $e |- Z = ( 0g ` W ) $.
    lmodvnegid.n $e |- N = ( invg ` W ) $.
    $( Addition of a vector with its negative. $)
    lmodvnegid $p |- ( ( W e. LMod /\ X e. V ) -> ( X P ( N ` X ) ) = Z ) $=
      ( clmod wcel cgrp cfv co wceq lmodgrp grprinv sylan ) DKLDMLECLEEBNAOFPDQ
      CADBFEGHIJRS $.
      $( [19-Jun-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    lmodvneg1.v $e |- V = ( Base ` W ) $.
    lmodvneg1.n $e |- N = ( invg ` W ) $.
    lmodvneg1.f $e |- F = ( Scalar ` W ) $.
    lmodvneg1.s $e |- S = ( vsca ` W ) $.
    lmodvneg1.u $e |- U = ( 1r ` F ) $.
    lmodvneg1.m $e |- M = ( invg ` F ) $.
    $( Minus 1 times a vector is the negative of the vector.  Equation 2 of
       [Kreyszig] p. 51. $)
    lmodvneg1 $p |- ( ( W e. LMod /\ X e. V ) ->
                      ( ( M ` U ) S X ) = ( N ` X ) ) $=
      ( wcel c0g cfv co wceq eqid clmod wa cplusg simpl cbs cgrp adantr lmod1cl
      lmodfgrp grpinvcl syl2anc simpr lmodvscl syl3anc lmodass syl13anc lmodvs1
      lmodvnegcl oveq2d grplinv oveq1d lmodvsdi2 syl121anc lmod0vs eqtr3d 0vlid
      3eqtr3d lmodvnegid syldan 0vrid 3eqtr3rd ) GUAOZHFOZUBZGPQZHEQZGUCQZRZBDQ
      ZHARZVOVQRZVPVTVNVTHVQRZVPVQRZVTHVPVQRZVQRZVRWAVNVLVTFOZVMVPFOZWCWESVLVMU
      DZVNVLVSCUEQZOZVMWFWHVNCUFOZBWIOZWJVLWKVMCGKUIUGZVLWLVMBCWIGKWITZMUHUGZWI
      CDBWNNUJUKZVLVMULZVSACWIFGHIKLWNUMUNZWQEFGHIJURZVQFGVTHVPIVQTZUOUPVNWBVOV
      PVQVNVTBHARZVQRZWBVOVNXAHVTVQABCFGHIKLMUQUSVNVSBCUCQZRZHARZCPQZHARXBVOVNX
      DXFHAVNWKWLXDXFSWMWOWIXCCDXFBWNXCTZXFTZNUTUKVAVNVLWJWLVMXEXBSWHWPWOWQVQXC
      VSBACWIFGHIWTKLWNXGVBVCACXFFGHVOIKLXHVOTZVDVGVEVAVNWDVOVTVQVQEFGHVOIWTXIJ
      VHUSVGVLVMWGVRVPSWSVQFGVPVOIWTXIVFVIVLVMWFWAVTSWRVQFGVTVOIWTXIVJVIVK $.
      $( [19-Jun-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    lmodvsubcl.v $e |- V = ( Base ` W ) $.
    lmodvsubcl.m $e |- M = ( -g ` W ) $.
    $( Closure of vector subtraction.  ( ~ hvsubcl analog.) $)
    lmodvsubcl $p |- ( ( W e. LMod /\ X e. V /\ Y e. V ) -> ( X M Y ) e. V ) $=
      ( clmod wcel cgrp co lmodgrp grpsubcl syl3an1 ) CHICJIDBIEBIDEAKBICLBCADE
      FGMN $.
      $( [19-Jun-2014] $) $( [31-Mar-2014] $)
  $}

  ${
    lmodcom.v $e |- V = ( Base ` W ) $.
    lmodcom.a $e |- A = ( +g ` W ) $.
    $( Left module vector sum is commutative.  (Contributed by G&eacute;rard
       Lang, 25-Jun-2014.) $)
    lmodcom $p |- ( ( W e. LMod /\ X e. V /\ Y e. V ) ->
                    ( X A Y ) = ( Y A X ) ) $=
      ( wcel wceq cfv eqid syl3anc lmodvacl lmodvsdi2 syl121anc lmodvs1 oveq12d
      co lmodass syl13anc clmod w3a csca cur cplusg cvsca cbs simp1 lmod1cl syl
      lmodacl simp2 simp3 lmodvsdi1 syl112anc eqtr3d syl2anc eqtrd 3eqtr3d cgrp
      3eqtr4d wb lmodgrp grprcan mpbid 3com23 lmodlcan ) CUAHZDBHZEBHZUBZDDEARZ
      ARZDEDARZARZIZVLVNIZVKDDARZEARZVLDARZVMVOVKVSEARZVTEARZIZVSVTIZVKVREEARZA
      RZVLVLARZWAWBVKCUCJZUDJZWIWHUEJZRZDCUFJZRZWKEWLRZARZWIVLWLRZWPARZWFWGVKWK
      VLWLRZWOWQVKVHWKWHUGJZHZVIVJWRWOIVHVIVJUHZVKVHWIWSHZXBWTXAVKVHXBXAWIWHWSC
      WHKZWSKZWIKZUIUJZXFWJWHWSCWIWIXCXDWJKZUKLVHVIVJULZVHVIVJUMZAWKWLWHWSBCDEF
      GXCWLKZXDUNUOVKVHXBXBVLBHZWRWQIXAXFXFABCDEFGMZAWJWIWIWLWHWSBCVLFGXCXJXDXG
      NOUPVKWMVRWNWEAVKWMWIDWLRZXMARZVRVKVHXBXBVIWMXNIXAXFXFXHAWJWIWIWLWHWSBCDF
      GXCXJXDXGNOVKXMDXMDAVKVHVIXMDIXAXHWLWIWHBCDFXCXJXEPUQZXOQURVKWNWIEWLRZXPA
      RZWEVKVHXBXBVJWNXQIXAXFXFXIAWJWIWIWLWHWSBCEFGXCXJXDXGNOVKXPEXPEAVKVHVJXPE
      IXAXIWLWIWHBCEFXCXJXEPUQZXRQURQVKWPVLWPVLAVKVHXKWPVLIXAXLWLWIWHBCVLFXCXJX
      EPUQZXSQUSVKVHVRBHZVJVJWAWFIXAVKVHVIVIXTXAXHXHABCDDFGMLZXIXIABCVREEFGSTVK
      VHXKVIVJWBWGIXAXLXHXIABCVLDEFGSTVAVKCUTHZVSBHZVTBHZVJWCWDVBVKVHYBXACVCUJV
      KVHXTVJYCXAYAXIABCVREFGMLVKVHXKVIYDXAXLXHABCVLDFGMLXIBACVSVTEFGVDTVEVKVHV
      IVIVJVSVMIXAXHXHXIABCDDEFGSTVKVHVIVJVIVTVOIXAXHXIXHABCDEDFGSTUSVKVHXKVNBH
      ZVIVPVQVBXAXLVHVJVIYEABCEDFGMVFXHABCVLVNDFGVGTVE $.
      $( [25-Jun-2014] $)
  $}

  ${
    $d x y W $.
    $( A left module is an abelian group (of vectors, under addition). $)
    lmodabl $p |- ( W e. LMod -> W e. Abel ) $=
      ( vx vy clmod wcel cbs cfv cplusg eqidd lmodgrp cv eqid lmodcom isabld )
      ADEZBCAFGZAHGZAOPIOQIAJQPABKCKPLQLMN $.
      $( [25-Jun-2014] $) $( [8-Dec-2013] $)

    $( A left module is a commutative monoid under addition. $)
    lmodcmn $p |- ( W e. LMod -> W e. CMnd ) $=
      ( clmod wcel cabel ccmn lmodabl ablcmn syl ) ABCADCAECAFAGH $.
      $( [7-Jan-2015] $)
  $}

  ${
    lmod4.v $e |- V = ( Base ` W ) $.
    lmod4.p $e |- P = ( +g ` W ) $.
    $( Commutative/associative law for left module vector sum. $)
    lmod4 $p |- ( ( W e. LMod /\ ( X e. V /\ Y e. V ) /\ ( Z e. V /\ U e. V ) )
        -> ( ( X P Y ) P ( Z P U ) ) = ( ( X P Z ) P ( Y P U ) ) ) $=
      ( clmod wcel ccmn wa co wceq lmodcmn cmn4 syl3an1 ) DJKDLKECKFCKMGCKBCKME
      FANGBANANEGANFBANANODPCADBEFGHIQR $.
      $( [19-Jun-2014] $) $( [4-Feb-2014] $)

    lmodvaddsub4.m $e |- M = ( -g ` W ) $.
    $( Relationship between vector subtraction and addition.  ( ~ hvsubadd
       analog.) $)
    lmodvsubadd $p |- ( ( W e. LMod /\ ( A e. V /\ B e. V /\ C e. V ) )
          -> ( ( A M B ) = C <-> ( B P C ) = A ) ) $=
      ( clmod wcel cabel w3a co wceq wb lmodabl ablsubadd sylan ) GKLGMLAFLBFLC
      FLNABEOCPBCDOAPQGRFDGEABCHIJST $.
      $( [19-Jun-2014] $) $( [31-Mar-2014] $)

    $( Vector addition/subtraction law. $)
    lmodvaddsub4 $p |- ( ( W e. LMod /\ ( A e. V /\ B e. V ) /\
                           ( C e. V /\ D e. V ) ) ->
      ( ( A P B ) = ( C P D ) <-> ( A M C ) = ( D M B ) ) ) $=
      ( clmod wcel cabel wa co wceq wb lmodabl abladdsub4 syl3an1 ) HLMHNMAGMBG
      MOCGMDGMOABEPCDEPQACFPDBFPQRHSGEHFDABCIJKTUA $.
      $( [19-Jun-2014] $) $( [31-Mar-2014] $)

    $( Addition/subtraction cancellation law for vectors.  ( ~ hvpncan
       analog.) $)
    lmodvpncan $p |- ( ( W e. LMod /\ A e. V /\ B e. V ) ->
                       ( ( A P B ) M B ) = A ) $=
      ( clmod wcel cgrp co wceq lmodgrp grppncan syl3an1 ) FJKFLKAEKBEKABCMBDMA
      NFOECFDABGHIPQ $.
      $( [19-Jun-2014] $) $( [16-Apr-2014] $)

    $( Cancellation law for vector subtraction ( ~ npcan analog). $)
    lmodvnpcan $p |- ( ( W e. LMod /\ A e. V /\ B e. V ) ->
                       ( ( A M B ) P B ) = A ) $=
      ( clmod wcel cgrp co wceq lmodgrp grpnpcan syl3an1 ) FJKFLKAEKBEKABDMBCMA
      NFOECFDABGHIPQ $.
      $( [19-Jun-2014] $) $( [19-Apr-2014] $)
  $}

  ${
    lmodvsubval2.v $e |- V = ( Base ` W ) $.
    lmodvsubval2.p $e |- P = ( +g ` W ) $.
    lmodvsubval2.m $e |- M = ( -g ` W ) $.
    lmodvsubval2.f $e |- F = ( Scalar ` W ) $.
    lmodvsubval2.s $e |- S = ( vsca ` W ) $.
    lmodvsubval2.n $e |- N = ( invg ` F ) $.
    lmodvsubval2.u $e |- U = ( 1r ` F ) $.
    $( Value of vector subtraction in terms of addition.  ( ~ hvsubval
       analog.)  (The proof was shortened by Mario Carneiro, 19-Jun-2014.) $)
    lmodvsubval2 $p |- ( ( W e. LMod /\ A e. V /\ B e. V )
          -> ( A M B ) = ( A P ( ( N ` U ) S B ) ) ) $=
      ( wcel co cfv clmod cminusg wceq eqid grpsubval 3adant1 lmodvneg1 3adant2
      w3a oveq2d eqtr4d ) JUARZAIRZBIRZUIZABGSZABJUBTZTZCSZAEHTBDSZCSUMUNUPUSUC
      ULICJUQGABKLUQUDZMUEUFUOUTURACULUNUTURUCUMDEFHUQIJBKVANOQPUGUHUJUK $.
      $( [19-Jun-2014] $) $( [31-Mar-2014] $)
  $}

  ${
    lmodvsubdi1.v $e |- V = ( Base ` W ) $.
    lmodvsubdi1.t $e |- T = ( vsca ` W ) $.
    lmodvsubdi1.f $e |- F = ( Scalar ` W ) $.
    lmodvsubdi1.k $e |- K = ( Base ` F ) $.
    lmodvsubdi1.m $e |- M = ( -g ` W ) $.
    lmodvsubdi1.w $e |- ( ph -> W e. LMod ) $.
    lmodvsubdi1.a $e |- ( ph -> A e. K ) $.
    lmodvsubdi1.x $e |- ( ph -> X e. V ) $.
    lmodvsubdi1.y $e |- ( ph -> Y e. V ) $.
    $( Scalar multiplication distributive law for subtraction.  ( ~ hvsubdistr1
       analog, with longer proof since our scalar multiplication is not
       commutative.) $)
    lmodvsubdi1 $p |- ( ph
       -> ( A T ( X M Y ) ) = ( ( A T X ) M ( A T Y ) ) ) $=
      ( co cur cfv cminusg cplusg wcel wceq cgrp crg lmodrng syl rnggrp rngidcl
      clmod grpinvcl syl2anc lmodvscl syl3anc lmodvsdi1 syl112anc cmulr rngnegr
      eqid rngnegl eqtr4d oveq1d lmodvsass syl121anc 3eqtr3d eqtrd lmodvsubval2
      oveq2d 3eqtr4d ) ABIDUAUBZDUCUBZUBZJCTZHUDUBZTZCTZBICTZVOBJCTZCTZVQTZBIJF
      TZCTVTWAFTZAVSVTBVPCTZVQTZWCAHUMUEZBEUEZIGUEZVPGUEZVSWGUFPQRAWHVOEUEZJGUE
      ZWKPADUGUEZVMEUEZWLADUHUEZWNAWHWPPDHMUIUJZDUKUJAWPWOWQEDVMNVMVBZULUJEDVNV
      MNVNVBZUNUOZSVOCDEGHJKMLNUPUQVQBCDEGHIVPKVQVBZMLNURUSAWFWBVTVQABVODUTUBZT
      ZJCTZVOBXBTZJCTZWFWBAXCXEJCAXCBVNUBXEAEDXBVMVNBNXBVBZWRWSWQQVAAEDXBVMVNBN
      XGWRWSWQQVCVDVEAWHWIWLWMXDWFUFPQWTSBVOCXBDEGHJKMLNXGVFVGAWHWLWIWMXFWBUFPW
      TQSVOBCXBDEGHJKMLNXGVFVGVHVKVIAWDVRBCAWHWJWMWDVRUFPRSIJVQCVMDFVNGHKXAOMLW
      SWRVJUQVKAWHVTGUEZWAGUEZWEWCUFPAWHWIWJXHPQRBCDEGHIKMLNUPUQAWHWIWMXIPQSBCD
      EGHJKMLNUPUQVTWAVQCVMDFVNGHKXAOMLWSWRVJUQVL $.
      $( [2-Jul-2014] $)
  $}

  ${
    lmodvsubdi2.v $e |- V = ( Base ` W ) $.
    lmodvsubdi2.t $e |- T = ( vsca ` W ) $.
    lmodvsubdi2.f $e |- F = ( Scalar ` W ) $.
    lmodvsubdi2.k $e |- K = ( Base ` F ) $.
    lmodvsubdi2.m $e |- M = ( -g ` W ) $.
    lmodvsubdi2.s $e |- S = ( -g ` F ) $.
    lmodvsubdi2.w $e |- ( ph -> W e. LMod ) $.
    lmodvsubdi2.a $e |- ( ph -> A e. K ) $.
    lmodvsubdi2.b $e |- ( ph -> B e. K ) $.
    lmodvsubdi2.x $e |- ( ph -> X e. V ) $.
    $( Scalar multiplication distributive law for subtraction.  ( ~ hvsubdistr2
       analog.) $)
    lmodvsubdi2 $p |- ( ph
       -> ( ( A S B ) T X ) = ( ( A T X ) M ( B T X ) ) ) $=
      ( co cur cfv cminusg cplusg wcel wceq eqid grpsubval syl2anc oveq1d clmod
      crg lmodrng syl rnggrp grpinvcl lmodvsdi2 syl121anc cmulr rngnegl rngidcl
      cgrp lmodvsass eqtr3d oveq2d 3eqtrd lmodvscl syl3anc lmodvsubval2 eqtr4d
      ) ABCDUBZKEUBZBKEUBZFUCUDZFUEUDZUDZCKEUBZEUBZJUFUDZUBZVOVSHUBZAVNBCVQUDZF
      UFUDZUBZKEUBZVOWDKEUBZWAUBZWBAVMWFKEABGUGZCGUGZVMWFUHSTGWEFVQDBCOWEUIZVQU
      IZQUJUKULAJUMUGZWJWDGUGZKIUGZWGWIUHRSAFVDUGZWKWOAFUNUGZWQAWNWRRFJNUOUPZFU
      QUPZTGFVQCOWMURUKUAWAWEBWDEFGIJKLWAUIZNMOWLUSUTAWHVTVOWAAVRCFVAUDZUBZKEUB
      ZWHVTAXCWDKEAGFXBVPVQCOXBUIZVPUIZWMWSTVBULAWNVRGUGZWKWPXDVTUHRAWQVPGUGZXG
      WTAWRXHWSGFVPOXFVCUPGFVQVPOWMURUKTUAVRCEXBFGIJKLNMOXEVEUTVFVGVHAWNVOIUGZV
      SIUGZWCWBUHRAWNWJWPXIRSUABEFGIJKLNMOVIVJAWNWKWPXJRTUACEFGIJKLNMOVIVJVOVSW
      AEVPFHVQIJLXAPNMWMXFVKVJVL $.
      $( [2-Jul-2014] $)
  $}

  ${
    lmodvsubeq0.v $e |- V = ( Base ` W ) $.
    lmodvsubeq0.o $e |- O = ( 0g ` W ) $.
    lmodvsubeq0.m $e |- M = ( -g ` W ) $.
    $( If the difference between two vectors is zero, they are equal.
       ( ~ hvsubeq0 analog.) $)
    lmodvsubeq0 $p |- ( ( W e. LMod /\ A e. V /\ B e. V )
                  -> ( ( A M B ) = O <-> A = B ) ) $=
      ( clmod wcel cgrp co wceq wb lmodgrp grpsubeq0 syl3an1 ) FJKFLKAEKBEKABCM
      DNABNOFPEFCDABGHIQR $.
      $( [19-Jun-2014] $) $( [31-Mar-2014] $)

    $( Subtraction of a vector from itself.  ( ~ hvsubid analog.) $)
    lmodvsubid $p |- ( ( W e. LMod /\ A e. V ) -> ( A M A ) = O ) $=
      ( clmod wcel cgrp co wceq lmodgrp grpsubid sylan ) EIJEKJADJAABLCMENDEBCA
      FGHOP $.
      $( [19-Jun-2014] $) $( [16-Apr-2014] $)
  $}

  ${
    $d q r w x y z B $.  $d q r w x y z K $.  $d q r w x y z ph $.
    $d q r w x y z L $.  $d q r w x y z P $.
    lmodpropd.1 $e |- ( ph -> B = ( Base ` K ) ) $.
    lmodpropd.2 $e |- ( ph -> B = ( Base ` L ) ) $.
    lmodpropd.3 $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
      ( x ( +g ` K ) y ) = ( x ( +g ` L ) y ) ) $.
    lmodpropd.4 $e |- ( ph -> F = ( Scalar ` K ) ) $.
    lmodpropd.5 $e |- ( ph -> F = ( Scalar ` L ) ) $.
    lmodpropd.6 $e |- P = ( Base ` F ) $.
    lmodpropd.7 $e |- ( ( ph /\ ( x e. P /\ y e. B ) ) ->
      ( x ( vsca ` K ) y ) = ( x ( vsca ` L ) y ) ) $.
    $( If two structures have the same components (properties), one is a left
       module iff the other one is.  (Contributed by Mario Carneiro,
       8-Feb-2015.) $)
    lmodpropd $p |- ( ph -> ( K e. LMod <-> L e. LMod ) ) $=
      ( wcel cfv co wral wceq vr vw vz vq cgrp csca crg cvsca w3a clmod lmodgrp
      cv wi a1i cbs cplusg cmulr cur wa eqid islmod simplr simprl fveq2d syl5eq
      simp2bi ad2antrr eleqtrd lmodvscl syl3anc eleqtrrd ralrimivva ex grppropd
      simprr syl5ibr eqtr3d eleq1d adantlr eleq12d mpbird adantr simpll simprlr
      3jcad wb simprrr proplem syl12anc simplr1 syl simprrl grpcl eqtrd simplr3
      oveq2d proplem2 syl21anc oveq12d eqeq12d simplr2 simprll rngacl 3anbi123d
      oveqd oveq1d rngcl rngidcl eqeq1d anbi12d anassrs 2ralbidva eleq2d anbi1d
      3anbi1d raleqbidv 3bitr3d 3bitr4g pm5.21ndd ) AGUEPZGUFQZUGPZBULZCULZGUHQ
      ZRZDPZCDSBESZUIZGUJPZHUJPZAYJXTYBYHYJXTUMAGUKUNYJYBUMAYJXTYBUAULZUBULZYER
      ZGUOQZPZYLYMUCULZGUPQZRZYERZYNYLYQYERZYRRZTZUDULZYLYAUPQZRZYMYERZUUDYMYER
      ZYNYRRZTZUIZUUDYLYAUQQZRZYMYERZUUDYNYERZTZYAURQZYMYERZYMTZUSZUSZUBYOSZUCY
      OSZUAYAUOQZSZUDUVDSZUCUBYRUUEYEUULUUQYAUVDYOGUAUDYOUTZYRUTZYEUTZYAUTZUVDU
      TZUUEUTZUULUTZUUQUTZVAZVFUNAYJYHAYJUSZYGBCEDUVPYCEPZYDDPZUSZUSZYFYODUVTYJ
      YCUVDPYDYOPYFYOPAYJUVSVBUVTYCEUVDUVPUVQUVRVCAEUVDTZYJUVSAEFUOQZUVDNAFYAUO
      LVDVEZVGVHUVTYDDYOUVPUVQUVRVOADYOTZYJUVSIVGZVHYCYEYAUVDYOGYDUVGUVJUVIUVKV
      IVJUWEVKVLVMWEAYKXTYBYHYKXTAHUEPZHUKABCDGHIJKVNZVPYKYBAHUFQZUGPZYKUWFUWIY
      LYMHUHQZRZHUOQZPZYLYMYQHUPQZRZUWJRZUWKYLYQUWJRZUWNRZTZUUDYLUWHUPQZRZYMUWJ
      RZUUDYMUWJRZUWKUWNRZTZUIZUUDYLUWHUQQZRZYMUWJRZUUDUWKUWJRZTZUWHURQZYMUWJRZ
      YMTZUSZUSZUBUWLSZUCUWLSZUAUWHUOQZSZUDUXSSZUCUBUWNUWTUWJUXGUXLUWHUXSUWLHUA
      UDUWLUTZUWNUTUWJUTZUWHUTZUXSUTZUWTUTUXGUTUXLUTVAZVFAYAUWHUGAFYAUWHLMVQZVR
      VPAYKYHAYKUSZYGBCEDUYHUVSUSZYGYCYDUWJRZUWLPZUYIYKYCUXSPYDUWLPUYKAYKUVSVBU
      YIYCEUXSUYHUVQUVRVCAEUXSTZYKUVSAEUWBUXSNAFUWHUOMVDVEZVGVHUYIYDDUWLUYHUVQU
      VRVOADUWLTZYKUVSJVGZVHYCUWJUWHUXSUWLHYDUYBUYDUYCUYEVIVJUYIYFUYJDUWLAUVSYF
      UYJTYKOVSUYOVTWAVLVMWEAYIYJYKWFAYIUSZXTYBUVFUIUWFUWIUYAUIYJYKUYPXTUWFYBUW
      IUVFUYAAXTUWFWFYIUWGWBUYPYAUWHUGAYAUWHTZYIUYGWBVRUYPYNDPZUUCUUJUIZUUTUSZU
      BDSZUCDSZUAESZUDESUWKDPZUWSUXEUIZUXOUSZUBDSZUCDSZUAESZUDESUVFUYAUYPVUBVUH
      UDUAEEUYPUUDEPZYLEPZUSZUSUYTVUFUCUBDDUYPVULYQDPZYMDPZUSZUYTVUFWFUYPVULVUO
      USZUSZUYSVUEUUTUXOVUQUYRVUDUUCUWSUUJUXEVUQYNUWKDVUQAVUKVUNYNUWKTAYIVUPWCZ
      UYPVUJVUKVUOWDZUYPVULVUMVUNWGZABCEDYEUWJYLYMOWHWIZVRVUQYTUWPUUBUWRVUQYTYL
      YSUWJRZUWPVUQAVUKYSDPYTVVBTVURVUSVUQYSYODVUQXTYMYOPYQYOPYSYOPXTYBYHAVUPWJ
      VUQYMDYOVUTVUQAUWDVURIWKZVHVUQYQDYOUYPVULVUMVUNWLZVVCVHYOYRGYMYQUVGUVHWMV
      JVVCVKABCEDYEUWJYLYSOWHWIVUQYSUWOYLUWJVUQAVUNVUMYSUWOTVURVUTVVDABCDDYRUWN
      YMYQKWHWIWPWNVUQUUBYNUUAUWNRZUWRVUQAUYRUUADPZUUBVVETVURVUQVUKVUNYHUYRVUSV
      UTXTYBYHAVUPWOZBCEDDYEYLYMWQWRZVUQVUKVUMYHVVFVUSVVDVVGBCEDDYEYLYQWQWRABCD
      DYRUWNYNUUAKWHWIVUQYNUWKUUAUWQUWNVVAVUQAVUKVUMUUAUWQTVURVUSVVDABCEDYEUWJY
      LYQOWHWIWSWNWTVUQUUGUXBUUIUXDVUQUUGUUFYMUWJRZUXBVUQAUUFEPVUNUUGVVITVURVUQ
      UUFUVDEVUQYBUUDUVDPZYLUVDPZUUFUVDPXTYBYHAVUPXAZVUQUUDEUVDUYPVUJVUKVUOXBZV
      UQAUWAVURUWCWKZVHZVUQYLEUVDVUSVVNVHZUVDUUEYAUUDYLUVKUVLXCVJVVNVKVUTABCEDY
      EUWJUUFYMOWHWIVUQUUFUXAYMUWJVUQUUEUWTUUDYLVUQYAUWHUPVUQAUYQVURUYGWKZVDXEX
      FWNVUQUUIUUHYNUWNRZUXDVUQAUUHDPZUYRUUIVVRTVURVUQVUJVUNYHVVSVVMVUTVVGBCEDD
      YEUUDYMWQWRVVHABCDDYRUWNUUHYNKWHWIVUQUUHUXCYNUWKUWNVUQAVUJVUNUUHUXCTVURVV
      MVUTABCEDYEUWJUUDYMOWHWIVVAWSWNWTXDVUQUUPUXKUUSUXNVUQUUNUXIUUOUXJVUQUUNUU
      MYMUWJRZUXIVUQAUUMEPVUNUUNVVTTVURVUQUUMUVDEVUQYBVVJVVKUUMUVDPVVLVVOVVPUVD
      YAUULUUDYLUVKUVMXGVJVVNVKVUTABCEDYEUWJUUMYMOWHWIVUQUUMUXHYMUWJVUQUULUXGUU
      DYLVUQYAUWHUQVVQVDXEXFWNVUQUUOUUDYNUWJRZUXJVUQAVUJUYRUUOVWATVURVVMVVHABCE
      DYEUWJUUDYNOWHWIVUQYNUWKUUDUWJVVAWPWNWTVUQUURUXMYMVUQUURUUQYMUWJRZUXMVUQA
      UUQEPVUNUURVWBTVURVUQUUQUVDEVUQYBUUQUVDPVVLUVDYAUUQUVKUVNXHWKVVNVKVUTABCE
      DYEUWJUUQYMOWHWIVUQUUQUXLYMUWJVUQYAUWHURVVQVDXFWNXIXJXJXKXLXLUYPVUCUVEUDE
      UVDAUWAYIUWCWBZUYPVUBUVCUAEUVDVWCUYPVUAUVBUCDYOAUWDYIIWBZUYPUYTUVAUBDYOVW
      DUYPUYSUUKUUTUYPUYRYPUUCUUJUYPDYOYNVWDXMXOXNXPXPXPXPUYPVUIUXTUDEUXSAUYLYI
      UYMWBZUYPVUHUXRUAEUXSVWEUYPVUGUXQUCDUWLAUYNYIJWBZUYPVUFUXPUBDUWLVWFUYPVUE
      UXFUXOUYPVUDUWMUWSUXEUYPDUWLUWKVWFXMXOXNXPXPXPXPXQXDUVOUYFXRVMXS $.
      $( [8-Feb-2015] $)
  $}

