$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Subgroups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SubGrp $.

  $( Extend class notation with all subgroups of a group. $)
  csubg $a class SubGrp $.

  ${
    $d w s $.
    $( Define a subgroup of a group as a set of elements that is a group in its
       own right. $)
    df-subg $a |- SubGrp = ( w e. Grp |->
        { s e. ~P ( Base ` w ) | ( w |`s s ) e. Grp } ) $.
  $}

  ${
    $d s w B $.  $d s w G $.  $d s S $.
    issubg.b $e |- B = ( Base ` G ) $.
    $( The subgroup predicate.  (Contributed by Mario Carneiro, 2-Dec-2014.) $)
    issubg $p |- ( S e. ( SubGrp ` G ) <->
      ( G e. Grp /\ S C_ B /\ ( G |`s S ) e. Grp ) ) $=
      ( vw vs csubg cfv wcel cgrp wss cress co cv cbs cpw crab wa wceq eleq1d
      w3a cdm df-subg dmmptss elfvdm sseldi simp1 fveq2 syl6eqr pweqd rabeqbidv
      oveq1 fvex eqeltri pwex rabex fvmpt eleq2d oveq2 elrab elpw2 anbi1i bitri
      cvv syl6bb ibar bitrd 3anass syl6bbr pm5.21nii ) BCGHZIZCJIZVMBAKZCBLMZJI
      ZUAZVLGUBJCEJENZFNZLMZJIZFVROHZPZQZGEFUCZUDBCGUEUFVMVNVPUGVMVLVMVNVPRZRZV
      QVMVLWFWGVMVLBCVSLMZJIZFAPZQZIZWFVMVKWKBECWDWKJGVRCSZWAWIFWCWJWMWBAWMWBCO
      HZAVRCOUHDUIUJWMVTWHJVRCVSLULTUKWEWIFWJAAWNVDDCOUMUNZUOUPUQURWLBWJIZVPRWF
      WIVPFBWJVSBSWHVOJVSBCLUSTUTWPVNVPBAWOVAVBVCVEVMWFVFVGVMVNVPVHVIVJ $.
      $( [2-Dec-2014] $)

    $( A subgroup is a subset.  (Contributed by Mario Carneiro, 2-Dec-2014.) $)
    subgss $p |- ( S e. ( SubGrp ` G ) -> S C_ B ) $=
      ( csubg cfv wcel cgrp wss cress co issubg simp2bi ) BCEFGCHGBAICBJKHGABCD
      LM $.
      $( [2-Dec-2014] $)

    $( A group is a subgroup of itself.  (Contributed by Mario Carneiro,
       7-Dec-2014.) $)
    subgid $p |- ( G e. Grp -> B e. ( SubGrp ` G ) ) $=
      ( cgrp wcel wss cress co csubg cfv id a1i ressid eqeltrd issubg syl3anbrc
      ssid ) BDEZRAAFZBAGHZDEABIJERKZSRAQLRTBDABDCMUANAABCOP $.
      $( [7-Dec-2014] $)
  $}

  ${
    subggrp.h $e |- H = ( G |`s S ) $.
    $( A subgroup is a group.  (Contributed by Mario Carneiro, 2-Dec-2014.) $)
    subggrp $p |- ( S e. ( SubGrp ` G ) -> H e. Grp ) $=
      ( csubg cfv wcel cress co cgrp cbs wss eqid issubg simp3bi syl5eqel ) ABE
      FGZCBAHIZJDQBJGABKFZLRJGSABSMNOP $.
      $( [2-Dec-2014] $)

    $( The base of the restricted group in a subgroup.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    subgbas $p |- ( S e. ( SubGrp ` G ) -> S = ( Base ` H ) ) $=
      ( csubg cfv wcel cbs wss wceq eqid subgss ressbas2 syl ) ABEFGABHFZIACHFJ
      OABOKZLAOCBDPMN $.
      $( [2-Dec-2014] $)
  $}

  $( Reverse closure for the subgroup predicate.  (Contributed by Mario
     Carneiro, 2-Dec-2014.) $)
  subgrcl $p |- ( S e. ( SubGrp ` G ) -> G e. Grp ) $=
    ( csubg cfv wcel cgrp cbs wss cress co eqid issubg simp1bi ) ABCDEBFEABGDZH
    BAIJFENABNKLM $.
    $( [2-Dec-2014] $)

  ${
    $d R x $.  $d S x $.  $d A x $.
    subg0.h $e |- H = ( G |`s S ) $.
    ${
      subg0.i $e |- O = ( 0g ` G ) $.
      $( A subgroup of a group must have the same identity as the group.
         (Contributed by Stefan O'Rear, 27-Nov-2014.)  (Revised by Mario
         Carneiro, 2-Dec-2014.) $)
      subg0 $p |- ( S e. ( SubGrp ` G ) -> O = ( 0g ` H ) ) $=
        ( cfv wcel c0g cplusg co wceq cgrp cbs subggrp eqid grpidcl syl syl2anc
        csubg ressplusg grplid wb subgrcl subgss subgbas eleqtrrd sseldd grpid
        mpbid ) ABTGHZCIGZULBJGZKULLZDULLZUKCMHZULCNGZHZUNABCEOZUKUPURUSUQCULUQ
        PZULPZQRZUQUMCULULUTAUMBCEUMPZUAVAUBSUKBMHULBNGZHUNUOUCABUDUKAVDULVDABV
        DPZUEUKULUQAVBABCEUFUGUHVDUMBDULVEVCFUISUJ $.
        $( [2-Dec-2014] $) $( [27-Nov-2014] $)
    $}

    subginv.i $e |- I = ( invg ` G ) $.
    subginv.j $e |- J = ( invg ` H ) $.
    $( The inverse of an element in a subgroup is the same as the inverse in
       the larger group. $)
    subginv $p |- ( ( S e. ( SubGrp ` G ) /\ X e. S ) ->
      ( I ` X ) = ( J ` X ) ) $=
      ( csubg cfv wcel wceq c0g cgrp cbs adantr eleq2d eqid sselda wa cplusg co
      subggrp subgbas biimpa ressplusg grprinv syl2anc eqtr4d wb subgrcl subgss
      subg0 wi grpinvcl ex syl 3imtr4d imp syldan grpinvid1 syl3anc mpbird ) AB
      JKLZFALZUAZFDKFEKZMZFVHBUBKZUCZBNKZMZVGVKCNKZVLVGCOLZFCPKZLZVKVNMVEVOVFAB
      CGUDZQVEVFVQVEAVPFABCGUEZRZUFVPVJCEVNFVPSZAVJBCGVJSZUGVNSIUHUIVEVLVNMVFAB
      CVLGVLSZUNQUJVGBOLZFBPKZLVHWELZVIVMUKVEWDVFABULQVEAWEFWEABWESZUMZTVEVFVHA
      LZWFVEVFWIVEVQVHVPLZVFWIVEVOVQWJUOVRVOVQWJVPCEFWAIUPUQURVTVEAVPVHVSRUSUTV
      EAWEVHWHTVAWEVJBDVLFVHWGWBWCHVBVCVD $.
      $( [2-Dec-2014] $)
  $}

  ${
    subg0cl.i $e |- O = ( 0g ` G ) $.
    $( The group identity is an element of any subgroup.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    subg0cl $p |- ( S e. ( SubGrp ` G ) -> O e. S ) $=
      ( csubg cfv wcel cress co c0g cbs cgrp eqid subggrp grpidcl subg0 subgbas
      syl eleq12d mpbird ) ABEFGZCAGBAHIZJFZUBKFZGZUAUBLGUEABUBUBMZNUDUBUCUDMUC
      MORUACUCAUDABUBCUFDPABUBUFQST $.
      $( [2-Dec-2014] $) $( [27-Nov-2014] $)
  $}

  ${
    subginvcl.i $e |- I = ( invg ` G ) $.
    $( The inverse of an element is closed in a subgroup. $)
    subginvcl $p |- ( ( S e. ( SubGrp ` G ) /\ X e. S ) -> ( I ` X ) e. S ) $=
      ( csubg cfv wcel wa cress cminusg cbs cgrp eqid subggrp adantr simpr wceq
      co subgbas eleqtrd grpinvcl syl2anc subginv eleq12d mpbird ) ABFGHZDAHZIZ
      DCGZAHDBAJSZKGZGZUKLGZHZUIUKMHZDUNHUOUGUPUHABUKUKNZOPUIDAUNUGUHQUGAUNRUHA
      BUKUQTPZUAUNUKULDUNNULNZUBUCUIUJUMAUNABUKCULDUQEUSUDURUEUF $.
      $( [2-Dec-2014] $)
  $}

  ${
    subgcl.p $e |- P = ( +g ` G ) $.
    $( A subgroup is closed under group operation.  (Contributed by Mario
       Carneiro, 2-Dec-2014.) $)
    subgcl $p |- ( ( S e. ( SubGrp ` G ) /\ X e. S /\ Y e. S ) ->
      ( X P Y ) e. S ) $=
      ( csubg cfv wcel w3a co cress cbs cgrp eqid subggrp 3ad2ant1 wceq eleqtrd
      simp2 subgbas simp3 ressplusg grpcl syl3anc eleqtrrd ) BCGHIZDBIZEBIZJZDE
      AKZCBLKZMHZBUJULNIZDUMIEUMIUKUMIUGUHUNUIBCULULOZPQUJDBUMUGUHUITUGUHBUMRUI
      BCULUOUAQZSUJEBUMUGUHUIUBUPSUMAULDEUMOBACULUOFUCUDUEUPUF $.
      $( [2-Dec-2014] $)
  $}

  ${
    $d u v w x y G $.  $d u v w x y I $.  $d u v w x y P $.  $d u v w x y S $.
    $d u v w B $.  $d u v w O $.
    issubg2.b $e |- B = ( Base ` G ) $.
    issubg2.o $e |- O = ( 0g ` G ) $.
    issubg2.p $e |- P = ( +g ` G ) $.
    issubg2.i $e |- I = ( invg ` G ) $.
    $( Characterize the subgroups of a group by closure properties.
       (Contributed by Mario Carneiro, 2-Dec-2014.) $)
    issubg2 $p |- ( G e. Grp -> ( S e. ( SubGrp ` G ) <-> ( S C_ B /\ S =/= (/)
      /\ A. x e. S ( A. y e. S ( x P y ) e. S /\ ( I ` x ) e. S ) ) ) ) $=
      ( vu wcel cfv cv co wa wceq eleq1d vv vw cgrp csubg wss c0 wne w3a subgss
      wral cress cbs eqid subgbas subggrp grpbn0 eqnetrd subgcl 3expa ralrimiva
      syl subginvcl jca 3jca simpr1 ressbas2 cplusg ressplusg a1i simpr3 ralimi
      simpl oveq1 oveq2 rcla42v syl5com 3impib 3anim123d imp grpass adantlr wex
      sseld syldan simpr2 n0 sylib sselda grplinv simpr fveq2 rcla4cva rcla42va
      sylan adantr syl21anc eqeltrrd exlimdv mpd grplid isgrpd issubg syl3anbrc
      ex impbid2 ) FUCNZEFUDONZECUEZEUFUGZAPZBPZDQZENZBEUJZXJGOZENZRZAEUJZUHZXG
      XHXIXRCEFIUIXGEFEUKQZULOZUFEFXTXTUMZUNXGXTUCNZYAUFUGEFXTYBUOYAXTYAUMUPVAU
      QXGXQAEXGXJENZRZXNXPYEXMBEXGYDXKENXMDEFXJXKKURUSUTEFGXJLVBVCUTVDXFXSXGXFX
      SRZXFXHYCXGXFXSVLXFXHXIXRVEZYFMUAUBEDXTMPZGOZHYFXHEYASYGECXTFYBIVFVADXTVG
      OSYFEDFXTYBKVHVIYFYHENZUAPZENZYHYKDQZENZYFXNAEUJZYJYLRYNYFXRYOXFXHXIXRVJZ
      XQXNAEXNXPVLVKVAZXMYNYHXKDQZENABYHYKEEXJYHSZXLYREXJYHXKDVMTXKYKSYRYMEXKYK
      YHDVNTVOVPVQYFYJYLUBPZENZUHZYHCNZYKCNZYTCNZUHZYMYTDQYHYKYTDQDQSZYFUUBUUFY
      FYJUUCYLUUDUUAUUEYFECYHYGWCYFECYKYGWCYFECYTYGWCVRVSXFUUFUUGXSCDFYHYKYTIKV
      TWAWDYFYJMWBZHENZYFXIUUHXFXHXIXRWEMEWFWGYFYJUUIMYFYJUUIYFYJRZYIYHDQZHEYFY
      JUUCUUKHSZYFECYHYGWHZXFUUCUULXSCDFGHYHIKJLWIWAWDZUUJYIENZYJYOUUKENZYFXPAE
      UJZYJUUOYFXRUUQYPXQXPAEXNXPWJVKVAXPUUOAYHEYSXOYIEXJYHGWKTWLWNZYFYJWJYFYOY
      JYQWOXMUUPYIXKDQZENABYIYHEEXJYISXLUUSEXJYIXKDVMTXKYHSUUSUUKEXKYHYIDVNTWMW
      PWQXDWRWSYFYJUUCHYHDQYHSZUUMXFUUCUUTXSCDFHYHIKJWTWAWDUURUUNXACEFIXBXCXDXE
      $.
      $( [2-Dec-2014] $)
  $}

  ${
    $d g x y G $.  $d g x y S $.
    $( The intersection of a nonempty collection of subgroups is a subgroup.
       (Contributed by Mario Carneiro, 7-Dec-2014.) $)
    subgint $p |- ( ( S C_ ( SubGrp ` G ) /\ S =/= (/) ) ->
      |^| S e. ( SubGrp ` G ) ) $=
      ( vx vy vg cfv wss c0 wne wcel wral adantlr eqid syl ralrimiva sylibr c0g
      wa cv elint2 csubg cint cbs cplusg co cminusg cuni intssuni adantl subgss
      ssel2 unissb sstrd subg0cl fvex simprl elinti sylan simprr subgcl syl3anc
      ne0i imp ovex anassrs adantll subginvcl syl2anc jca cgrp w3a ssn0 subgrcl
      wb wex n0 exlimiv sylbi issubg2 3syl mpbir3and ) ABUAFZGZAHIZRZAUBZWBJZWF
      BUCFZGZWFHIZCSZDSZBUDFZUEZWFJZDWFKZWKBUFFZFZWFJZRZCWFKZWEWFAUGZWHWDWFXBGW
      CAUHUIWEESZWHGZEAKXBWHGWEXDEAWEXCAJZRZXCWBJZXDWCXEXGWDAWBXCUKLZWHXCBWHMZU
      JNOEAWHULPUMWEBQFZWFJZWJWEXJXCJZEAKXKWEXLEAXFXGXLXHXCBXJXJMZUNNOEXJABQUOT
      PWFXJVBNWEWTCWFWEWKWFJZRZWPWSXOWODWFWEXNWLWFJZWOWEXNXPRZRZWNXCJZEAKWOXRXS
      EAXRXERXGWKXCJZWLXCJZXSWEXEXGXQXHLXRXNXEXTWEXNXPUPXNXEXTWKAXCUQVCZURXRXPX
      EYAWEXNXPUSXPXEYAWLAXCUQVCURWMXCBWKWLWMMZUTVAOEWNAWKWLWMVDTPVEOXOWRXCJZEA
      KWSXOYDEAXOXERXGXTYDWEXEXGXNXHLXNXEXTWEYBVFXCBWQWKWQMZVGVHOEWRAWKWQUOTPVI
      OWEWBHIZBVJJZWGWIWJXAVKVNAWBVLYFXGEVOYGEWBVPXGYGEXCBVMVQVRCDWHWMWFBWQXJXI
      XMYCYEVSVTWA $.
      $( [7-Dec-2014] $)
  $}

  ${
    $d x y H $.  $d s u v w y z ph $.  $d s u v w z R $.  $d s u v w x y z X $.
    $d u v x y G $.  $d s u v w x y z Y $.
    lagsubg.1 $e |- X = ( Base ` G ) $.
    ${
      lagsubg.2 $e |- H = ( G |`s Y ) $.
      lagsubg.3 $e |- R = { <. x , y >. |
        ( ( x e. X /\ y e. X ) /\ ( x ( -g ` G ) y ) e. Y ) } $.
      lagsubg.4 $e |- ( ph -> Y e. ( SubGrp ` G ) ) $.
      $( The coset relation is an equivalence relation. $)
      lagsubglem1 $p |- ( ph -> ( Er R /\ dom R = X ) ) $=
        ( wceq cv wbr wa wcel cfv co eleq1 vu vv wer cdm csg simpr vex bi2anan9
        vw oveq12 eleq1d anbi12d braba sylib simpld simprd cminusg cgrp subgrcl
        jca csubg syl adantr eqid grpsubinv syl3anc subginvcl eqeltrrd sylanbrc
        syl2anc adantrr simprr cplusg grpnpncan syl13anc idi subgcl iserd copab
        wss cxp simpl ssopab2i df-xp 3sstr4i dmss ax-mp dmxpid sseqtri grpsubid
        wi a1i c0g sylan subg0cl eqeltrd breldm ex ssrdv eqssd ) ADUCDUDZGMAUAU
        BUIDAUANZUBNZDOZPZXCGQZXBGQZPZXCXBEUERZSZHQZXCXBDOXEXFXGXEXGXFXEXGXFPZX
        BXCXISZHQZXEXDXLXNPZAXDUFBNZGQZCNZGQZPZXPXRXISZHQZPZXOBCXBXCDUAUGZUBUGZ
        XPXBMZXRXCMZPZXTXLYBXNYFXQXGYGXSXFXPXBGTZXRXCGTUHYHYAXMHXPXBXRXCXIUJUKU
        LKUMUNZUOZUPZXEXGXFYKUOZUTXEXMEUQRZRZXJHXEEURQZXGXFYOXJMAYPXDAHEVARQZYP
        LHEUSVBZVCYMYLGEXIYNXBXCIXIVDZYNVDZVEVFXEYQXNYOHQAYQXDLVCXEXLXNYJUPZHEY
        NXMYTVGVJVHYCXHXKPBCXCXBDYEYDXPXCMZXRXBMZPZXTXHYBXKUUBXQXFUUCXSXGXPXCGT
        ZXRXBGTZUHUUDYAXJHXPXCXRXBXIUJUKULKUMVIAXDXCUINZDOZPZPZXGUUGGQZPZXBUUGX
        ISZHQZXBUUGDOUUJXGUUKAXDXGUUHYMVKZUUJXFUUKUUJXFUUKPZXCUUGXISZHQZUUJUUHU
        UPUURPZAXDUUHVLYCUUSBCXCUUGDYEUIUGZUUBXRUUGMZPZXTUUPYBUURUUBXQXFUVAXSUU
        KUUEXRUUGGTZUHUVBYAUUQHXPXCXRUUGXIUJUKULKUMUNZUOZUPZUTUUJXMUUQEVMRZSZUU
        MHUUJYPXGXFUUKUVHUUMMAYPUUIYRVCUUOUUJXFUUKUVEUOUVFGUVGEXIXBXCUUGIUVGVDZ
        YSVNVOUUJYQXNUURUVHHQUUJYQWKAYQUUILVCVPUUJXNWKAXDXNUUHUUAVKVPUUJUURWKUU
        JUUPUURUVDUPVPUVGHEXMUUQUVIVQVFVHYCUULUUNPBCXBUUGDYDUUTYFUVAPZXTUULYBUU
        NYFXQXGUVAXSUUKYIUVCUHUVJYAUUMHXPXBXRUUGXIUJUKULKUMVIVRAXAGXAGVTAXAGGWA
        ZUDZGDUVKVTXAUVLVTYCBCVSXTBCVSDUVKYCXTBCXTYBWBWCKBCGGWDWEDUVKWFWGGWHWIW
        LAUAGXAAXGXBXAQZAXGPZXBXBDOZUVMUVNXGXGPZXBXBXISZHQZUVOUVNXGXGAXGUFZUVSU
        TUVNUVQEWMRZHAYPXGUVQUVTMYRGEXIUVTXBIUVTVDZYSWJWNAUVTHQZXGAYQUWBLHEUVTU
        WAWOVBVCWPYCUVPUVRPBCXBXBDYDYDYFUUCPZXTUVPYBUVRYFXQXGUUCXSXGYIUUFUHUWCY
        AUVQHXPXBXRXBXIUJUKULKUMVIXBXBDYDWQVBWRWSWTUT $.
        $( [8-Jul-2014] $)

      lagsubg.5 $e |- ( ph -> X e. Fin ) $.
      $( Lemma for ~ lagsubg . $)
      lagsubglem2 $p |- ( ph -> ( # ` X ) =
        ( ( # ` ( X /. R ) ) x. ( # ` Y ) ) ) $=
        ( chash cfv co wceq wcel wa wi vw vs vz vu vv cqs cuni cmul cfn wer cdm
        lagsubglem1 simpld simprd cxp wss syl2anc cv csg copab opabssxp eqsstri
        xpfi ssfi sylancl uniqs2 fveq2d ssid cpw pwfi sylib qsss c0 sseq1 unieq
        csn cun fveq2 oveq1d eqeq12d imbi12d imbi2d cc0 cn0 cc csubg subgss syl
        hashcl nn0cn mul02 3syl eqcomd fveq2i hash0 eqtri oveq1i 3eqtr4g a1d wn
        uni0 ssun1 sstr imim1i caddc oveq1 uniun vex unisn adantr simprr syl5ss
        mpan cin sylibr sseldd ad2antrr eqeltrd syl5bi syl3anc cen wbr eqid cvv
        simplr simpr eleq1 bi2anan9 oveq12 eleq1d anbi12d braba ex a1i wb eqcom
        elec oveq2d c1 a2d uneq2i sstrd sspwuni ssun2 snss elpwi simprlr eluni2
        elin wrex w3a wne simpr3 simpr2 inelcm simpr1 qsdisj ord necon1ad 3exp2
        mpd rexlimdv imp3a mtod eq0rdv hashun cec breq2 cplusg elex cgrp sselda
        subgrcl grpcl grppncan ovex sylanbrc ersym ersymb syl5bb simprbi syl6bi
        jca ecss simprl grpsubadd syl13anc 3bitr4g ectocld syldan hashen mpbird
        en3d eqtr4d syl5eq hashunsng ax-mp ad2antrl ax-1cn adddir mulid2 3eqtrd
        simprll syl5ibr expr syl5 expcom findcard2s mpcom mpi eqtr3d ) AGDUFZUG
        ZNOZGNOUXLNOZHNOZUHPZAUXMGNAGDUIADUJZDUKGQZABCDEFGHIJKLULZUMZAUXRUXSUXT
        UNZAGGUOZUIRZDUYCUPDUIRAGUIRZUYEUYDMMGGVCUQDBURZGRZCURZGRZSZUYFUYHEUSOZ
        PZHRZSZBCUTUYCKUYMBCGGVAVBUYCDVDVEZVFVGAUXLUXLUPZUXNUXQQZUXLVHUXLUIRZAU
        YPUYQTZAGVIZUIRZUXLUYTUPZUYRAUYEVUAMGVJVKAGDUIUYAUYBUYOVLZUYTUXLVDUQAUA
        URZUXLUPZVUDUGZNOZVUDNOZUXPUHPZQZTZTAVMUXLUPZVMUGZNOZVMNOZUXPUHPZQZTZTA
        UBURZUXLUPZVUSUGZNOZVUSNOZUXPUHPZQZTZTAVUSUCURZVPZVQZUXLUPZVVIUGZNOZVVI
        NOZUXPUHPZQZTZTAUYSTUAUBUCUXLVUDVMQZVUKVURAVVQVUEVULVUJVUQVUDVMUXLVNVVQ
        VUGVUNVUIVUPVVQVUFVUMNVUDVMVOVGVVQVUHVUOUXPUHVUDVMNVRVSVTWAWBVUDVUSQZVU
        KVVFAVVRVUEVUTVUJVVEVUDVUSUXLVNVVRVUGVVBVUIVVDVVRVUFVVANVUDVUSVOVGVVRVU
        HVVCUXPUHVUDVUSNVRVSVTWAWBVUDVVIQZVUKVVPAVVSVUEVVJVUJVVOVUDVVIUXLVNVVSV
        UGVVLVUIVVNVVSVUFVVKNVUDVVIVOVGVVSVUHVVMUXPUHVUDVVINVRVSVTWAWBVUDUXLQZV
        UKUYSAVVTVUEUYPVUJUYQVUDUXLUXLVNVVTVUGUXNVUIUXQVVTVUFUXMNVUDUXLVOVGVVTV
        UHUXOUXPUHVUDUXLNVRVSVTWAWBAVUQVULAWCWCUXPUHPZVUNVUPAVWAWCAUXPWDRZUXPWE
        RZVWAWCQAHUIRZVWBAUYEHGUPZVWDMAHEWFORZVWELGHEIWGWHZGHVDUQZHWIWHZUXPWJZU
        XPWKWLWMVUNVUOWCVUMVMNXAWNWOWPVUOWCUXPUHWOWQWRWSVUSUIRZVVGVUSRZWTZSZAVV
        FVVPAVWNVVFVVPTVVFVVJVVETAVWNSZVVPVVJVUTVVEVUSVVIUPVVJVUTVUSVVHXBZVUSVV
        IUXLXCXMXDVWOVVJVVEVVOAVWNVVJVVEVVOTVVEVVOAVWNVVJSZSZVVBUXPXEPZVVDUXPXE
        PZQVVBVVDUXPXEXFVWRVVLVWSVVNVWTVWRVVLVVAVVGVQZNOZVWSVVKVXANVVKVVAVVHUGZ
        VQVXAVUSVVHXGVXCVVGVVAVVGUCXHZXIUUAWPWNVWRVXBVVBVVGNOZXEPZVWSVWRVVAUIRZ
        VVGUIRZVVAVVGXNZVMQVXBVXFQVWRUYEVVAGUPZVXGAUYEVWQMXJZVWRVUSUYTUPVXJVWRV
        USUXLUYTVWRVUSVVIUXLVWPAVWNVVJXKZXLZAVUBVWQVUCXJZUUBVUSGUUCVKGVVAVDUQVW
        RUYEVVGGUPZVXHVXKVWRVVGUYTRVXOVWRUXLUYTVVGVXNVWRVVHUXLUPVVGUXLRZVWRVVHV
        VIUXLVVHVUSUUDVXLXLVVGUXLVXDUUEXOZXPVVGGUUFWHGVVGVDUQZVWRUAVXIVWRVUDVXI
        RZVWLAVWKVWMVVJUUGVXSVUDVVARZVUDVVGRZSVWRVWLVUDVVAVVGUUIVWRVXTVYAVWLVXT
        VUDUDURZRZUDVUSUUJVWRVYAVWLTZUDVUDVUSUUHVWRVYCVYDUDVUSVWRVYBVUSRZVYCVYA
        VWLVWRVYEVYCVYAUUKZSZVVGVYBVUSVYGVVGVYBXNZVMUULZVVGVYBQZVYGVYAVYCVYIVWR
        VYEVYCVYAUUMVWRVYEVYCVYAUUNVUDVVGVYBUUOUQVYGVYJVYHVMVYGVYJVYHVMQVYGGVVG
        VYBDAUXRVWQVYFUYAXQVWRVXPVYFVXQXJVYGVUSUXLVYBVWRVUTVYFVXMXJVWRVYEVYCVYA
        UUPZXPUUQUURUUSUVAVYKXRUUTUVBXSUVCXSUVDUVEVVAVVGUVFXTVWRUXPVXEVVBXEVWRU
        XPVXEQZHVVGYAYBZAVWQVXPVYMVXQHVUDDUVGZYAYBVYMAUAVVGGDUXLUXLYCVYNVVGHYAU
        VHAVUDGRZSZUDUEHVYNVYBVUDEUVIOZPZUEURZVUDUYKPZVYPVWDHYDRAVWDVYOVWHXJHUI
        UVJWHVYPVYBHRZVYRVYNRZVYPWUASZVUDVYRDYBWUBWUCVYRVUDDGGAUXRVYOWUAUYAXQWU
        CEUVKRZVYBGRZVYOVYRGRZAWUDVYOWUAAVWFWUDLHEUVMWHZXQZVYPHGVYBAVWEVYOVWGXJ
        UVLZAVYOWUAYEZGVYQEVYBVUDIVYQYCZUVNXTZWUJWUCWUFVYOSZVYRVUDUYKPZHRZVYRVU
        DDYBWUCWUFVYOWULWUJUWCWUCWUNVYBHWUCWUDWUEVYOWUNVYBQWUHWUIWUJGVYQEUYKVYB
        VUDIWUKUYKYCZUVOXTVYPWUAYFXRUYNWUMWUOSBCVYRVUDDVYBVUDVYQUVPZUAXHZUYFVYR
        QZUYHVUDQZSZUYJWUMUYMWUOWUSUYGWUFWUTUYIVYOUYFVYRGYGUYHVUDGYGZYHWVAUYLWU
        NHUYFVYRUYHVUDUYKYIYJYKKYLUVQUVRVYRVUDDWUQWURYQXOYMVYPVYSVYNRZVYSVUDDYB
        ZVYTHRZWVCVUDVYSDYBVYPWVDVYSVUDDUEXHZWURYQVYPVUDVYSDGYDAUXRVYOUYAXJAVYO
        YFVYSYDRVYPWVFYNUVSUVTWVDVYSGRZVYOSZWVEUYNWVHWVESBCVYSVUDDWVFWURUYFVYSQ
        ZWUTSZUYJWVHUYMWVEWVIUYGWVGWUTUYIVYOUYFVYSGYGWVBYHWVJUYLVYTHUYFVYSUYHVU
        DUYKYIYJYKKYLUWAUWBVYPWUAWVCSZVYBVYTQZVYSVYRQZYOVYPWVKSZVYTVYBQZVYRVYSQ
        ZWVLWVMWVNWUDWVGVYOWUEWVOWVPYOAWUDVYOWVKWUGXQWVNVYNGVYSAVYNGUPVYOWVKAVU
        DDGUYAUYBUWDXQVYPWUAWVCXKXPAVYOWVKYEWVNHGVYBAVWEVYOWVKVWGXQVYPWUAWVCUWE
        XPGVYQEUYKVYSVUDVYBIWUKWUPUWFUWGVYBVYTYPVYSVYRYPUWHYMUWMUWIUWJVWRVWDVXH
        VYLVYMYOAVWDVWQVWHXJVXRHVVGUWKUQUWLYRUWNUWOVWRVVNVVCYSXEPZUXPUHPZVVDYSU
        XPUHPZXEPZVWTVWRVVMWVQUXPUHVWNVVMWVQQZAVVJVVGYDRVWNWWATVXDVUSVVGYDUWPUW
        QUWRVSVWRVVCWERZYSWERZVWCWVRWVTQVWRVWKVVCWDRWWBAVWKVWMVVJUXCVUSWIVVCWJW
        LWWCVWRUWSYNVWRVWBVWCAVWBVWQVWIXJVWJWHZVVCYSUXPUWTXTVWRWVSUXPVVDXEVWRVW
        CWVSUXPQWWDUXPUXAWHYRUXBVTUXDUXEYTUXFUXGYTUXHUXIUXJUXK $.
        $( [11-Jul-2014] $)
    $}

    $( Lagrange theorem for Groups: the order of any subgroup of a finite group
       is a divisor of the order of the group.  (Contributed by Mario Carneiro,
       11-Jul-2014.)  (Revised by Mario Carneiro, 2-Dec-2014.) $)
    lagsubg $p |- ( ( Y e. ( SubGrp ` G ) /\ X e. Fin ) ->
                    ( # ` Y ) || ( # ` X ) ) $=
      ( vx vy cfv wcel cfn wa chash cv co cdivides cz cn0 wss eqid syl2anc ssfi
      csubg csg copab cqs cmul wbr cpw simpr pwfi sylib cress simpl lagsubglem1
      wer cdm wceq simpld simprd xpfi opabssxp sylancl qsss hashcl nn0z 3syl id
      cxp subgss syl2anr dvdsmul2 lagsubglem2 breqtrrd ) CAUAGHZBIHZJZCKGZBELZB
      HFLZBHJVQVRAUBGMCHZJEFUCZUDZKGZVPUEMZBKGNVOWBOHZVPOHZVPWCNUFVOWAIHZWBPHWD
      VOBUGZIHZWAWGQWFVOVNWHVMVNUHZBUIUJVOBVTIVOVTUNZVTUOBUPZVOEFVTAACUKMZBCDWL
      RZVTRZVMVNULZUMZUQVOWJWKWPURVOBBVGZIHZVTWQQVTIHVOVNVNWRWIWIBBUSSVSEFBBUTW
      QVTTVAVBWGWATSWAVCWBVDVEVOCIHZVPPHWEVNVNCBQWSVMVNVFBCADVHBCTVICVCVPVDVEWB
      VPVJSVOEFVTAWLBCDWMWNWOWIVKVL $.
      $( [2-Dec-2014] $) $( [11-Jul-2014] $)
  $}

