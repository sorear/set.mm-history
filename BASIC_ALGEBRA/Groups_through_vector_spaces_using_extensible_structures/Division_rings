$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Division rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c DivRing $.

  $( Extend class notation with class of all division rings. $)
  cdr $a class DivRing $.

  $( Define class of all division rings.  A division ring is a ring in which
     multiplication forms a group on the base set minus the ring zero element.
     Note that we construct an explicit 2-element group structure member to
     specify this condition. $)
  df-drng $a |- DivRing = { r e. Ring |
    { <. ( Base ` ndx ) , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. ,
           <. ( +g ` ndx ) , ( .r ` r ) >. } e. Grp } $.

  ${
    $d r G $.  $d r R $.
    isdivrng.b $e |- B = ( Base ` R ) $.
    isdivrng.t $e |- T = ( .r ` R ) $.
    isdivrng.z $e |- Z = ( 0g ` R ) $.
    isdivrng.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
          <. ( +g ` ndx ) , T >. } $.
    $( The predicate "is a division ring". $)
    isdrng $p |- ( R e. DivRing <-> ( R e. Ring /\ G e. Grp ) ) $=
      ( vr cbs cfv c0g csn cop cmulr cgrp wcel fveq2 syl6eqr cnx cv cdif cplusg
      cpr crg cdr wceq sneqd sneqi opeq2d preq12d eleq1d eleq1i syl6bbr df-drng
      difeq12d elrab2 ) UAKLZJUBZKLZUTMLZNZUCZOZUAUDLZUTPLZOZUEZQRZDQRZJBUFUGUT
      BUHZVJUSAENZUCZOZVFCOZUEZQRVKVLVIVQQVLVEVOVHVPVLVDVNUSVLVAAVCVMVLVABKLAUT
      BKSFTVLVCBMLZNVMVLVBVRUTBMSUIEVRHUJTUQUKVLVGCVFVLVGBPLCUTBPSGTUKULUMDVQQI
      UNUOJUPUR $.
      $( [18-Oct-2012] $)
  $}

  $( A division ring is a ring. $)
  drngrng $p |- ( R e. DivRing -> R e. Ring ) $=
    ( cdr wcel crg cnx cbs cfv c0g csn cdif cop cplusg cmulr cpr isdrng simplbi
    cgrp eqid ) ABCADCEFGAFGZAHGZIJKELGAMGZKNZQCSAUAUBTSRUARTRUBROP $.
    $( [8-Sep-2011] $)

  $( A division ring is a group. $)
  drnggrp $p |- ( R e. DivRing -> R e. Grp ) $=
    ( cdr wcel crg cgrp drngrng rnggrp syl ) ABCADCAECAFAGH $.
    $( [8-Sep-2011] $)

  ${
    $d u x K $.  $d u x L $.
    dvrngprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    dvrngprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    dvrngprop.m $e |- ( .r ` K ) = ( .r ` L ) $.
    $( If two structures have the same ring components (properties), one is a
       division ring iff the other one is. $)
    drngprop $p |- ( K e. DivRing <-> L e. DivRing ) $=
      ( vu vx cvv wcel cdr wn cbs cfv c0 wceq cgrp syl cop wa eqid wb con3i wne
      elex fvprc drnggrp grpn0 necon2bi 2falsed eqcomi crg cnx c0g cplusg cmulr
      csn cdif cpr isdrng cv co wral crio grpidval adantr adantl eqtr4d difeq2d
      sneqd opeq2d preq1d eleq1d anbi2d rngprop anbi1i 3bitr4g syl5bb ecase ) A
      HIZBHIZAJIZBJIZUAVSKZWAWBWAVSAJUDUBWCALMZNOWBKALUEWBWDNWBBPIWDNUCBUFWDBCU
      GQUHQUIVTKZWAWBWEBLMZNOWAKBLUEWAWFNWAAPIWFNUCAUFWFAWDWFCUJUGQUHQWBVTBJUDU
      BUIWAAUKIZULLMZWDAUMMZUPZUQZRZULUNMAUOMZRZURZPIZSZVSVTSZWBWDAWMWOWIWDTZWM
      TWITZWOTUSWRBUKIZWPSXAWHWDBUMMZUPZUQZRZWNURZPIZSWQWBWRWPXGXAWRWOXFPWRWLXE
      WNWRWKXDWHWRWJXCWDWRWIXBWRWIFUTGUTZAUNMZVAXHOGWDVBFWDVCZXBVSWIXJOVTGWDXIF
      AWIHWSXITWTVDVEVTXBXJOVSGWDXIFBXBHCDXBTZVDVFVGVIVHVJVKVLVMWGXAWPABCDEVNVO
      WDBWMXFXBCEXKXFTUSVPVQVR $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z T $.  $d x U $.  $d x y z Z $.  $d x y z ph $.
    isdivrngd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isdivrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isdivrngd.z $e |- ( ph -> Z = ( 0g ` R ) ) $.
    isdivrngd.u $e |- ( ph -> U = ( 1r ` R ) ) $.
    isdivrngd.r $e |- ( ph -> R e. Ring ) $.
    isdivrngd.n $e |- ( ( ph /\ ( x e. B /\ x =/= Z )
            /\ ( y e. B /\ y =/= Z ) ) -> ( x T y ) =/= Z ) $.
    isdivrngd.o $e |- ( ph -> U =/= Z ) $.
    isdivrngd.i $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I e. B ) $.
    isdivrngd.j $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I =/= Z ) $.
    ${
      isdivrngd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( I T x ) = U ) $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ." $)
      isdrngd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz crg cnx cbs cfv c0g csn cdif cop cplusg cmulr cpr cgrp wa cdr
        sneqd difeq12d opeq2d preq12d eleq1d anbi2d cvv wceq fvex syl6eqel eqid
        difexg grpbase 3syl grpplusg syl prex a1i cv wne co eldifsn w3a syl3an1
        3expib eleq2d anbi12d eleq12d 3imtr4d 3impib 3adant2r 3adant3r sylanbrc
        rngcl oveqd syl3an3b syl3an2b eldifi 3anim123i wi ex 3anbi123d oveq123d
        rngass eqeq12d imp sylan2 rngidcl mpbird rnglidm eqeq1d adantrr sylan2b
        eqidd cur isgrpd mpbi2and isdrng sylibr ) AEUBTZUCUDUEZEUDUEZEUFUEZUGZU
        HZUIZUCUJUEZEUKUEZUIZULZUMTZUNZEUOTAXOXPDIUGZUHZUIZYBFUIZULZUMTZYGAYMYF
        XOAYLYEUMAYJYAYKYDAYIXTXPADXQYHXSJAIXRLUPUQURAFYCYBKURUSUTVANABCUAYIFYL
        HGADVBTYIVBTYIYLUDUEVCADXQVBJEUDVDVEDYHVBVGYIFYLVBYLVFZVHVIAFVBTFYLUJUE
        VCAFYCVBKEUKVDVEYIFYLVBYNVJVKYLVBTAYJYKVLVMBVNZYITZAYODTZYOIVOZUNZCVNZY
        ITZYOYTFVPZYITZYODIVQZUUAAYSYTDTZYTIVOZUNZUUCYTDIVQAYSUUGVRUUBDTZUUBIVO
        UUCAYSUUEUUHUUFAYQUUEUUHYRAYQUUEUUHAYOXQTZYTXQTZUNYOYTYCVPZXQTZYQUUEUNU
        UHAUUIUUJUULAXOUUIUUJUULNXQEYCYOYTXQVFZYCVFZWIVSVTAYQUUIUUEUUJADXQYOJWA
        ZADXQYTJWAZWBAUUBUUKDXQAFYCYOYTKWJZJWCWDWEWFWGOUUBDIVQWHWKWLYPUUAUAVNZY
        ITZVRAYQUUEUURDTZVRZUUBUURFVPZYOYTUURFVPZFVPZVCZYPYQUUAUUEUUSUUTYODYHWM
        YTDYHWMUURDYHWMWNAUVAUVEAUUIUUJUURXQTZVRZUUKUURYCVPZYOYTUURYCVPZYCVPZVC
        ZUVAUVEAXOUVGUVKWONXOUVGUVKXQEYCYOYTUURUUMUUNWSWPVKAYQUUIUUEUUJUUTUVFUU
        OUUPADXQUURJWAWQAUVBUVHUVDUVJAUUBUUKUURUURFYCKUUQAUURXIWRAYOYOUVCUVIFYC
        KAYOXIZAFYCYTUURKWJWRWTWDXAXBAGDTZGIVOGYITAUVMEXJUEZXQTZAXOUVONXQEUVNUU
        MUVNVFZXCVKAGUVNDXQMJWCXDPGDIVQWHYPAYSGYOFVPZYOVCZUUDAYQUVRYRAYQUVRAUUI
        UVNYOYCVPZYOVCZYQUVRAXOUUIUVTWONXOUUIUVTXQEYCUVNYOUUMUUNUVPXEWPVKUUOAUV
        QUVSYOAGUVNYOYOFYCKMUVLWRXFWDXAXGXHYPAYSHYITZUUDAYSUNHDTHIVOUWAQRHDIVQW
        HXHYPAYSHYOFVPGVCUUDSXHXKXLXQEYCYEXRUUMUUNXRVFYEVFXMXN $.
        $( [2-Aug-2013] $)
    $}

    ${
      $d y z I $.
      isdivrngrd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( x T I ) = U )
          $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ."  In this
         version of ~ isdrngd we are use right reciprocal of ~ isgrprd rather
         than the left reciprocal of ~ isgrpd . $)
      isdrngrd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz cv wne wa csn cdif co wceq eldifsn w3a cbs cfv cmulr crg eqid
        rngcl syl3an1 3expib anbi12d eleq12d 3imtr4d 3adant2r 3adant3r sylanbrc
        eleq2d 3impib syl3an3b syl3an2b cur rngidcl syl eleqtrrd eqeltrd eldifi
        oveqd eqidd oveq123d adantr rngridm sylan ex sylbid imp eqtrd 3anim123i
        sylan2 3anbi123d wi rngass 3eqtr4d sylan2b grplinvd sylan2br isdrngd )
        ABCDEFGHIJKLMNOPQRBUBZDTZWOIUCZUDZAWODIUEZUFZTZHWOFUGGUHWODIUIZABCUAWTF
        HGXAAWRCUBZWTTZWOXCFUGZWTTZXBXDAWRXCDTZXCIUCZUDZXFXCDIUIAWRXIUJXEDTZXEI
        UCXFAWRXGXJXHAWPXGXJWQAWPXGXJAWOEUKULZTZXCXKTZUDWOXCEUMULZUGZXKTZWPXGUD
        XJAXLXMXPAEUNTZXLXMXPNXKEXNWOXCXKUOZXNUOZUPUQURAWPXLXGXMADXKWOJVEZADXKX
        CJVEZUSAXEXODXKAFXNWOXCKVOZJUTVAVFVBVCOXEDIUIVDVGVHAGDTGIUCGWTTAGEVIULZ
        DMAYCXKDAXQYCXKTNXKEYCXRYCUOZVJVKJVLVMPGDIUIVDXAAWPWOGFUGZWOUHWODWSVNZA
        WPUDYEWOYCXNUGZWOAYEYGUHWPAWOWOGYCFXNKAWOVPZMVQVRAWPYGWOUHZAWPXLYIXTAXL
        YIAXQXLYINXKEXNYCWOXRXSYDVSVTWAWBWCWDWFXAXDUAUBZWTTZUJAWPXGYJDTZUJZXEYJ
        FUGZWOXCYJFUGZFUGZUHXAWPXDXGYKYLYFXCDWSVNYJDWSVNWEAYMUDXOYJXNUGZWOXCYJX
        NUGZXNUGZYNYPAYMYQYSUHZAYMXLXMYJXKTZUJZYTAWPXLXGXMYLUUAXTYAADXKYJJVEWGA
        XQUUBYTWHNXQUUBYTXKEXNWOXCYJXRXSWIWAVKWBWCAYNYQUHYMAXEXOYJYJFXNKYBAYJVP
        VQVRAYPYSUHYMAWOWOYOYRFXNKYHAFXNXCYJKVOVQVRWJWFXAAWRHWTTZXBAWRUDHDTHIUC
        UUCQRHDIUIVDWKXAAWRWOHFUGGUHXBSWKWLWMWN $.
        $( [10-Aug-2013] $)
    $}
  $}

  ${
    divrngmgrp.b $e |- B = ( Base ` R ) $.
    divrngmgrp.t $e |- T = ( .r ` R ) $.
    divrngmgrp.z $e |- Z = ( 0g ` R ) $.
    divrngmgrp.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( A division ring contains a multiplicative group. $)
    drngmgrp $p |- ( R e. DivRing -> G e. Grp ) $=
      ( cdr wcel crg cgrp isdrng simprbi ) BJKBLKDMKABCDEFGHINO $.
      $( [8-Sep-2011] $)
  $}

  ${
    divrngmcl.b $e |- B = ( Base ` R ) $.
    divrngmcl.t $e |- T = ( .r ` R ) $.
    divrngmcl.z $e |- Z = ( 0g ` R ) $.
    $( The product of two nonzero elements of a division ring is nonzero. $)
    drngmcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } )
                    /\ Y e. ( B \ { Z } ) ) -> ( X T Y ) e. ( B \ { Z } ) ) $=
      ( wcel cnx cbs cfv cop cplusg cvv wceq fvex eqeltri ax-mp cdr csn cdif co
      cpr cgrp eqid drngmgrp difexg grpbase cmulr grpplusg grpcl syl3an1 ) BUAJ
      KLMAFUBZUCZNKOMCNUEZUFJDUPJEUPJDECUDUPJABCUQFGHIUQUGZUHUPCUQDEUPPJZUPUQLM
      QAPJUSABLMPGBLRSAUOPUITUPCUQPURUJTCPJCUQOMQCBUKMPHBUKRSUPCUQPURULTUMUN $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngid.b $e |- B = ( Base ` R ) $.
    divrngid.t $e |- T = ( .r ` R ) $.
    divrngid.z $e |- Z = ( 0g ` R ) $.
    divrngid.u $e |- U = ( 1r ` R ) $.
    divrngid.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( Lemma for ~ drngid and ~ drngunz . $)
    drngidlem $p |- ( R e. DivRing -> U =/= Z ) $=
      ( wcel cfv wne cvv cbs wceq 3syl co syl2anc cdr c0g cgrp csn cdif eqeltri
      drngmgrp fvex difexg grpbase mp2b eqid grpidcl eldifsn simprbi wa drngrng
      crg eldifi rnglidm oveq1 sylan9req rnglz adantr eqtrd ex necon3d mpd ) BU
      ALZEUBMZFNZDFNVIEUCLZVJAFUDZUEZLZVKABCEFGHIKUGZVNEVJAOLVNOLVNEPMQABPMOGBP
      UHUFAVMOUIVNCEOKUJUKVJULUMZVOVJALZVKVJAFUNUORVIDFVJFVIDFQZVJFQVIVSUPVJFVJ
      CSZFVIVSVJDVJCSZVTVIBURLZVRWAVJQBUQZVIVLVOVRVPVQVJAVMUSRZABCDVJGHJUTTDFVJ
      CVAVBVIVTFQZVSVIWBVRWEWCWDABCVJFGHIVCTVDVEVFVGVH $.
      $( [8-Sep-2011] $)

    $( A division ring's unit is the identity element of its multiplicative
       group. $)
    drngid $p |- ( R e. DivRing -> U = ( 0g ` G ) ) $=
      ( wcel cfv co wceq cvv cbs fvex ax-mp syl cdr c0g crg drngrng csn eqeltri
      cdif cgrp difexg grpbase eqid grpidcl eldifi rnglidm syl2anc cplusg cmulr
      drngmgrp grpplusg grplid eqtr4d wb wn rngidcl drngidlem df-ne sylib elsni
      wne nsyl eldif sylanbrc grprcan syl13anc mpbid ) BUALZDEUBMZCNZVQVQCNZOZD
      VQOZVPVRVQVSVPBUCLZVQALZVRVQOBUDZVPVQAFUEZUGZLZWCVPEUHLZWGABCEFGHIKURZWFE
      VQWFPLZWFEQMOAPLWJABQMPGBQRUFAWEPUISWFCEPKUJSZVQUKZULTZVQAWEUMTABCDVQGHJU
      NUOVPWHWGVSVQOWIWMWFCEVQVQWKCPLCEUPMOCBUQMPHBUQRUFWFCEPKUSSZWLUTUOVAVPWHD
      WFLZWGWGVTWAVBWIVPDALZDWELZVCWOVPWBWPWDABDGJVDTVPDFOZWQVPDFVIWRVCABCDEFGH
      IJKVEDFVFVGDFVHVJDAWEVKVLWMWMWFCEDVQVQWKWNVMVNVO $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngunz.z $e |- Z = ( 0g ` R ) $.
    divrngunz.u $e |- U = ( 1r ` R ) $.
    $( A division ring's unit is different from its zero. $)
    drngunz $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cbs cfv cmulr cnx csn cdif cop cplusg cpr eqid drngidlem ) AFGZAAHGZBIF
      GQCJKLIMGRLNZCQORODESOP $.
      $( [8-Sep-2011] $)
  $}

  ${
    dvrngid2.b $e |- B = ( Base ` R ) $.
    dvrngid2.t $e |- T = ( .r ` R ) $.
    dvrngid2.o $e |- Z = ( 0g ` R ) $.
    dvrngid2.u $e |- U = ( 1r ` R ) $.
    $( Properties showing that an element ` I ` is the identity element of a
       division ring. $)
    drngid2 $p |- ( R e. DivRing
        -> ( ( I e. B /\ I =/= Z /\ ( I T I ) = I ) <-> U = I ) ) $=
      ( wcel wceq cnx cbs cfv cop cplusg wa eqid cvv cdr wne co w3a csn cpr c0g
      cdif df-3an eldifsn anbi1i bitr4i cgrp wb drngmgrp eqeltri difexg grpbase
      fvex mp2b cmulr grpplusg ax-mp isgrpid2 syl syl5bb drngid eqeq1d bitr4d )
      BUAKZEAKZEFUBZEECUCELZUDZMNOAFUEZUHZPMQOCPUFZUGOZELZDELVNEVPKZVMRZVJVSVNV
      KVLRZVMRWAVKVLVMUIVTWBVMEAFUJUKULVJVQUMKWAVSUNABCVQFGHIVQSZUOVPCVQVREATKV
      PTKVPVQNOLABNOTGBNUSUPAVOTUQVPCVQTWCURUTCTKCVQQOLCBVAOTHBVAUSUPVPCVQTWCVB
      VCVRSVDVEVFVJDVREABCDVQFGHIJWCVGVHVI $.
      $( [11-Oct-2013] $)
  $}

  $c invr $.

  $( Extend class notation with multiplicative inverse. $)
  cinvr $a class invr $.

  $( Define multiplicative inverse. $)
  df-invr $a |- invr = ( r e. _V |-> ( invg `
      { <. ( Base ` ndx ) , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. ,
        <. ( +g ` ndx ) , ( .r ` r ) >. } ) ) $.

  ${
    $d r N $.  $d r R $.
    invrfval.b $e |- B = ( Base ` R ) $.
    invrfval.z $e |- Z = ( 0g ` R ) $.
    invrfval.t $e |- T = ( .r ` R ) $.
    invrfval.4 $e |- G =
           { <. ( Base ` ndx ) , ( B \ { Z } ) >. , <. ( +g ` ndx ) , T >. } $.
    invrfval.n $e |- N = ( invg ` G ) $.
    invrfval.i $e |- I = ( invr ` R ) $.
    $( Multiplicative inverse function for a division ring. $)
    invrfval $p |- ( R e. A -> I = N ) $=
      ( vr cfv cbs cop cminusg syl6eqr wcel cinvr cvv wceq elex cnx cv c0g cdif
      csn cplusg cmulr cpr fveq2 difeq12d opeq2d preq12d fveq2d df-invr eqeltri
      sneqd fvex fvmpt syl syl5eq ) CAUAZFCUBPZGNVFCUCUAVGGUDCAUEOCUFQPZOUGZQPZ
      VIUHPZUJZUIZRZUFUKPZVIULPZRZUMZSPZGUCUBVICUDZVSESPZGVTVRESVTVRVHBHUJZUIZR
      ZVODRZUMEVTVNWDVQWEVTVMWCVHVTVJBVLWBVTVJCQPBVICQUNITVTVKHVTVKCUHPHVICUHUN
      JTVAUOUPVTVPDVOVTVPCULPDVICULUNKTUPUQLTURMTOUSGWAUCMESVBUTVCVDVE $.
      $( [21-Sep-2011] $)
  $}

  ${
    invrcl.b $e |- B = ( Base ` R ) $.
    invrcl.z $e |- Z = ( 0g ` R ) $.
    invrcl.i $e |- I = ( invr ` R ) $.
    $( Closure of the multiplicative inverse in a division ring. $)
    invrcllem $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } ) )
             -> ( I ` X ) e. ( B \ { Z } ) ) $=
      ( cdr wcel csn cdif wa cfv cnx cbs cop wceq eqid cvv cplusg cmulr cminusg
      cpr invrfval fveq1d adantr cgrp drngmgrp fvex eqeltri difexg grpbase mp2b
      grpinvcl sylan eqeltrd ) BIJZDAEKZLZJZMDCNZDOPNUTQOUANBUBNZQUDZUCNZNZUTUR
      VBVFRVAURDCVEIABVCVDCVEEFGVCSZVDSZVESZHUEUFUGURVDUHJVAVFUTJABVCVDEFVGGVHU
      IUTVDVEDATJUTTJUTVDPNRABPNTFBPUJUKAUSTULUTVCVDTVHUMUNVIUOUPUQ $.
      $( [21-Sep-2011] $)

    $( Closure of the multiplicative inverse in a division ring.  ( ~ reccl
       analog.) $)
    drnginvrcl $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( I ` X ) e. B ) $=
      ( cdr wcel wne cfv wa csn cdif eldifsn invrcllem eldifi syl sylan2br
      3impb ) BIJZDAJZDEKZDCLZAJZUCUDMUBDAENZOZJZUFDAEPUBUIMUEUHJUFABCDEFGHQUEA
      UGRSTUA $.
      $( [19-Apr-2014] $)

    $( The multiplicative inverse in a division ring is nonzero.  ( ~ recne0
       analog.) $)
    drnginvrn0 $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( I ` X ) =/= Z ) $=
      ( cdr wcel wne cfv wa csn cdif eldifsn invrcllem simprbi syl sylan2br
      3impb ) BIJZDAJZDEKZDCLZEKZUCUDMUBDAENOZJZUFDAEPUBUHMUEUGJZUFABCDEFGHQUIU
      EAJUFUEAEPRSTUA $.
      $( [19-Apr-2014] $)
  $}

  ${
    invrl.b $e |- B = ( Base ` R ) $.
    invrl.z $e |- Z = ( 0g ` R ) $.
    invrl.t $e |- T = ( .r ` R ) $.
    invrl.u $e |- U = ( 1r ` R ) $.
    invrl.i $e |- I = ( invr ` R ) $.
    $( Property of the multiplicative inverse in a division ring.  ( ~ recid2
       analog.) $)
    drnginvrl $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( ( I ` X ) T X ) = U ) $=
      ( cdr wcel cbs cfv wceq eqid 3ad2ant1 cvv wne w3a cnx csn cdif cop cplusg
      cpr cminusg co c0g cgrp drngmgrp simp2 simp3 eldifsn sylanbrc fvex difexg
      eqeltri grpbase mp2b cmulr grpplusg ax-mp grplinv syl2anc invrfval fveq1d
      oveq1d drngid 3eqtr4d ) BMNZFANZFGUAZUBZFUCOPAGUDZUEZUFUCUGPCUFUHZUIPZPZF
      CUJZVSUKPZFEPZFCUJZDVPVSULNZFVRNZWBWCQVMVNWFVOABCVSGHJIVSRZUMSVPVNVOWGVMV
      NVOUNVMVNVOUOFAGUPUQVRCVSVTWCFATNVRTNVRVSOPQABOPTHBOURUTAVQTUSVRCVSTWHVAV
      BCTNCVSUGPQCBVCPTJBVCURUTVRCVSTWHVDVEWCRVTRZVFVGVMVNWEWBQVOVMWDWAFCVMFEVT
      MABCVSEVTGHIJWHWILVHVIVJSVMVNDWCQVOABCDVSGHJIKWHVKSVL $.
      $( [19-Apr-2014] $)

    $( Property of the multiplicative inverse in a division ring.  ( ~ recid
       analog.) $)
    drnginvrr $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( X T ( I ` X ) ) = U ) $=
      ( cdr wcel cbs cfv wceq eqid 3ad2ant1 cvv wne w3a cnx csn cdif cop cplusg
      cpr cminusg co c0g cgrp drngmgrp simp2 simp3 eldifsn sylanbrc fvex difexg
      eqeltri grpbase mp2b cmulr grpplusg ax-mp grprinv syl2anc invrfval fveq1d
      oveq2d drngid 3eqtr4d ) BMNZFANZFGUAZUBZFFUCOPAGUDZUEZUFUCUGPCUFUHZUIPZPZ
      CUJZVSUKPZFFEPZCUJZDVPVSULNZFVRNZWBWCQVMVNWFVOABCVSGHJIVSRZUMSVPVNVOWGVMV
      NVOUNVMVNVOUOFAGUPUQVRCVSVTWCFATNVRTNVRVSOPQABOPTHBOURUTAVQTUSVRCVSTWHVAV
      BCTNCVSUGPQCBVCPTJBVCURUTVRCVSTWHVDVEWCRVTRZVFVGVMVNWEWBQVOVMWDWAFCVMFEVT
      MABCVSEVTGHIJWHWILVHVIVJSVMVNDWCQVOABCDVSGHJIKWHVKSVL $.
      $( [19-Apr-2014] $)
  $}

  $c /r $.

  $( Extend class notation with ring division. $)
  cdvr $a class /r $.

  ${
    $d r x y $.
    $( Define ring division. $)
    df-drngdv $a |- /r = ( r e. _V
     |-> ( x e. ( Base ` r ) , y e. ( ( Base ` r ) \ { ( 0g ` r ) } )
         |-> ( x ( .r ` r ) ( ( invr ` r ) ` y ) ) ) ) $.
  $}

  ${
    $d r x y B $.  $d r I $.  $d r x y O $.  $d r x y R $.  $d r T $.
    drngdvval.b $e |- B = ( Base ` R ) $.
    drngdvval.t $e |- T = ( .r ` R ) $.
    drngdvval.o $e |- O = ( 0g ` R ) $.
    drngdvval.i $e |- I = ( invr ` R ) $.
    drngdvval.d $e |- D = ( /r ` R ) $.
    drngdvval.r $e |- ( ph -> R e. V ) $.
    $( Division operation for a division ring. $)
    drngdvfval $p |- ( ph
          -> D = ( x e. B , y e. ( B \ { O } ) |-> ( x T ( I ` y ) ) ) ) $=
      ( wcel cvv cfv cbs vr csn cdif cv co cmpt2 wceq elex cdvr c0g cinvr cmulr
      fveq2 syl6eqr sneqd difeq12d eqidd oveq123d mpt2eq123dv df-drngdv eqeltri
      fveq1d fvex difexg ax-mp mpt2ex fvmpt syl5eq 3syl ) AFJQFRQZEBCDDIUBZUCZB
      UDZCUDZHSZGUEZUFZUGPFJUHVJEFUISVQOUAFBCUAUDZTSZVSVRUJSZUBZUCZVMVNVRUKSZSZ
      VRULSZUEZUFVQRUIVRFUGZBCVSWBWFDVLVPWGVSFTSZDVRFTUMKUNZWGVSDWAVKWIWGVTIWGV
      TFUJSIVRFUJUMMUNUOUPWGVMVMWDVOWEGWGWEFULSGVRFULUMLUNWGVMUQWGVNWCHWGWCFUKS
      HVRFUKUMNUNVBURUSBCUAUTBCDVLVPDWHRKFTVCVAZDRQVLRQWJDVKRVDVEVFVGVHVI $.
      $( [2-Jul-2014] $)

    $d x y I $.  $d x y T $.  $d x y X $.  $d y Y $.
    drngdvval.x $e |- ( ph -> X e. B ) $.
    drngdvval.y $e |- ( ph -> Y e. B ) $.
    drngdvval.n $e |- ( ph -> Y =/= O ) $.
    $( Division operation for a division ring. $)
    drngdvval $p |- ( ph -> ( X D Y ) = ( X T ( I ` Y ) ) ) $=
      ( co vx vy csn cdif cfv cmpt2 drngdvfval oveqd wcel wceq wne c0g cvv fvex
      cv eqeltri elsnc2 necon3bbii sylibr eldif sylanbrc oveq1 oveq2d eqid ovex
      wn fveq2 ovmpt2 syl2anc eqtrd ) AIJCTIJUAUBBBGUCZUDZUAUOZUBUOZFUEZETZUFZT
      ZIJFUEZETZACVQIJAUAUBBCDEFGHKLMNOPUGUHAIBUIJVLUIZVRVTUJQAJBUIJVKUIZVFZWAR
      AJGUKWCSWBJGJGGDULUEUMMDULUNUPUQURUSJBVKUTVAUAUBIJBVLVPVTVQIVOETVMIVOEVBV
      NJUJVOVSIEVNJFVGVCVQVDIVSEVEVHVIVJ $.
      $( [2-Jul-2014] $)
  $}

  ${
    drngdvcl.b $e |- B = ( Base ` R ) $.
    drngdvcl.o $e |- O = ( 0g ` R ) $.
    drngdvcl.d $e |- D = ( /r ` R ) $.
    drngdvcl.r $e |- ( ph -> R e. DivRing ) $.
    drngdvcl.x $e |- ( ph -> X e. B ) $.
    drngdvcl.y $e |- ( ph -> Y e. B ) $.
    drngdvcl.n $e |- ( ph -> Y =/= O ) $.
    $( Closure of division operation. $)
    drngdvcl $p |- ( ph -> ( X D Y ) e. B ) $=
      ( co cfv cdr eqid wcel syl3anc cinvr drngdvval crg drngrng syl drnginvrcl
      cmulr wne rngcl eqeltrd ) AFGCOFGDUAPZPZDUGPZOZBABCDUMUKEQFGHUMRZIUKRZJKL
      MNUBADUCSZFBSULBSZUNBSADQSZUQKDUDUELAUSGBSGEUHURKMNBDUKGEHIUPUFTBDUMFULHU
      OUITUJ $.
      $( [2-Jul-2014] $)
  $}

  ${
    drngdvcan1.b $e |- B = ( Base ` R ) $.
    drngdvcan1.t $e |- T = ( .r ` R ) $.
    drngdvcan1.o $e |- O = ( 0g ` R ) $.
    drngdvcan1.d $e |- D = ( /r ` R ) $.
    drngdvcan1.r $e |- ( ph -> R e. DivRing ) $.
    drngdvcan1.x $e |- ( ph -> X e. B ) $.
    drngdvcan1.y $e |- ( ph -> Y e. B ) $.
    drngdvcan1.n $e |- ( ph -> Y =/= O ) $.
    $( A cancellation law for division.  ( ~ divcan1 analog.) $)
    drngdvcan1 $p |- ( ph -> ( ( X D Y ) T Y ) = X ) $=
      ( co cfv wcel wceq cinvr cdr eqid drngdvval oveq1d crg drngrng drnginvrcl
      syl wne syl3anc rngass syl13anc drnginvrl oveq2d rngridm syl2anc eqtrd
      cur ) AGHCQZHEQGHDUARZRZEQZHEQZGAUTVCHEABCDEVAFUBGHIJKVAUCZLMNOPUDUEAVDGV
      BHEQZEQZGADUFSZGBSZVBBSZHBSZVDVGTADUBSZVHMDUGUIZNAVLVKHFUJZVJMOPBDVAHFIKV
      EUHUKOBDEGVBHIJULUMAVGGDUSRZEQZGAVFVOGEAVLVKVNVFVOTMOPBDEVOVAHFIKJVOUCZVE
      UNUKUOAVHVIVPGTVMNBDEVOGIJVQUPUQURURUR $.
      $( [2-Jul-2014] $)
  $}

