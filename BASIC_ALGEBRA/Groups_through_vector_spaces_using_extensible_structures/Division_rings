$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Division rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c DivRing $.

  $( Extend class notation with class of all division rings. $)
  cdr $a class DivRing $.

  $( Define class of all division rings.  A division ring is a ring in which
     the set of units is exactly the nonzero elements of the ring. $)
  df-drng $a |- DivRing = { r e. Ring |
    ( Unit ` r ) = ( ( Base ` r ) \ { ( 0g ` r ) } ) } $.

  ${
    $d r B $.  $d r R $.  $d r U $.  $d r Z $.
    isdrng.b $e |- B = ( Base ` R ) $.
    isdrng.u $e |- U = ( Unit ` R ) $.
    isdrng.z $e |- Z = ( 0g ` R ) $.
    $( The predicate "is a division ring".  (Revised by Mario Carneiro,
       2-Dec-2014.) $)
    isdrng $p |- ( R e. DivRing <-> ( R e. Ring /\ U = ( B \ { Z } ) ) ) $=
      ( vr cv cui cfv cbs c0g csn cdif wceq crg cdr fveq2 syl6eqr sneqd eqeq12d
      difeq12d df-drng elrab2 ) HIZJKZUFLKZUFMKZNZOZPCADNZOZPHBQRUFBPZUGCUKUMUN
      UGBJKCUFBJSFTUNUHAUJULUNUHBLKAUFBLSETUNUIDUNUIBMKDUFBMSGTUAUCUBHUDUE $.
      $( [2-Dec-2014] $) $( [18-Oct-2012] $)

    $( Elementhood in the set of units when ` R ` is a division ring. $)
    drngunit $p |- ( R e. DivRing -> ( X e. U <-> ( X e. B /\ X =/= Z ) ) ) $=
      ( cdr wcel csn cdif wne wa crg wceq isdrng simprbi eleq2d eldifsn syl6bb
      ) BIJZDCJDAEKLZJDAJDEMNUBCUCDUBBOJCUCPABCEFGHQRSDAETUA $.
      $( [2-Dec-2014] $)
  $}

  ${
    $d r B $.  $d r R $.  $d r U $.  $d r Z $.
    drngui.b $e |- B = ( Base ` R ) $.
    drngui.z $e |- Z = ( 0g ` R ) $.
    drngui.r $e |- R e. DivRing $.
    $( The set of units of a division ring. $)
    drngui $p |- ( B \ { Z } ) = ( Unit ` R ) $=
      ( cui cfv csn cdif crg wcel wceq cdr wa eqid isdrng mpbi simpri eqcomi )
      BGHZACIJZBKLZUAUBMZBNLUCUDOFABUACDUAPEQRST $.
      $( [2-Dec-2014] $)
  $}

  $( A division ring is a ring. $)
  drngrng $p |- ( R e. DivRing -> R e. Ring ) $=
    ( cdr wcel crg cui cfv cbs c0g csn cdif wceq eqid isdrng simplbi ) ABCADCAE
    FZAGFZAHFZIJKPAOQPLOLQLMN $.
    $( [8-Sep-2011] $)

  $( A division ring is a group. $)
  drnggrp $p |- ( R e. DivRing -> R e. Grp ) $=
    ( cdr wcel crg cgrp drngrng rnggrp syl ) ABCADCAECAFAGH $.
    $( [8-Sep-2011] $)

  ${
    $d x y B $.  $d x y G $.  $d x y R $.  $d y T $.  $d x Z $.
    isdrng2.b $e |- B = ( Base ` R ) $.
    isdrng2.z $e |- Z = ( 0g ` R ) $.
    isdrng2.g $e |- G = ( ( mulGrp ` R ) |`s ( B \ { Z } ) ) $.
    $( A division ring can equivalently be defined as a ring such that the
       nonzero elements form a group under multiplication (from which it
       follows that this is the same group as the group of units).
       (Contributed by Mario Carneiro, 2-Dec-2014.) $)
    isdrng2 $p |- ( R e. DivRing <-> ( R e. Ring /\ G e. Grp ) ) $=
      ( vx wcel cfv wceq wa cgrp eqid cress co adantl adantr syl2anc wbr isdrng
      cdr crg cui csn cdif cmgp syl6eqr unitgrp eqeltrrd cv wne unitcl c0g cdvr
      oveq2 cmulr wss cbs difss mgpbas ressbas2 grpidcl ad2antlr eldifsn simprd
      ax-mp sylib wi simpll idi simpr rngdvcan1 syl3anc rngdvcl 3netr4d necon3i
      simpld rngrz syl sylanbrc ex ssrdv cur cdsr coppr cminusg eldifi grpinvcl
      adantll dvdsrmul mgpplusg ressplusg grplinv rngidcl mpdan wb 1unit sseldd
      rnglidm grpid mpbid eqtrd breqtrd opprbas grprinv opprmul 3eqtr3d impbida
      isunit eqssd pm5.32i bitri ) BUBIBUCIZBUDJZADUEZUFZKZLZXNCMIZLZABXODEXONZ
      FUAXNXRXTXNXRXTXSBUGJZXOOPZCMXSYDYCXQOPZCXRYDYEKXNXOXQYCOUPQGUHXNYDMIXRBX
      OYDYBYDNUIRUJYAXOXQYAHXOXQYAHUKZXOIZYFXQIZYAYGLZYFAIZYFDULZYHYGYJYAABXOYF
      EYBUMQYICUNJZYFBUOJZPZYFBUQJZPZYNDYOPZULYKYIYLDYPYQYIYLAIZYLDULZYIYLXQIZY
      RYSLXTYTXNYGXQCYLXQAURXQCUSJKAXPUTXQACYCGABYCYCNZEVAVBVGZYLNZVCVDYLADVEVH
      ZVFYIXNYRYGYPYLKYIXNVIXNXTYGVJZVKYIYRVIYIYRYSUUDVRZVKYIYGVIYAYGVLZVKAYMBY
      OXOYLYFEYBYMNZYONZVMVNYIXNYNAIZYQDKUUEYIXNYRYGUUJUUEUUFUUGAYMBXOYLYFEYBUU
      HVOVNABYOYNDEUUIFVSSVPYFDYPYQYFDYNYOUPVQVTYFADVEWAWBWCZYAHXQXOYAYHYGYAYHL
      ZYFBWDJZBWEJZTYFUUMBWFJZWEJZTYGUULYFYFCWGJZJZYFYOPZUUMUUNUULYJUURAIZYFUUS
      UUNTYHYJYAYFAXPWHQZUULUURXQIZUUTXTYHUVBXNXQCUUQYFUUBUUQNZWIWJUURAXPWHVTZA
      UUNBYOYFUUREUUNNZUUIWKSUULUUSYLUUMXTYHUUSYLKXNXQYOCUUQYLYFUUBXQYOYCCGBYOY
      CUUAUUIWLWMZUUCUVCWNWJYAYLUUMKZYHYAUUMUUMYOPUUMKZUVGXNUVHXTXNUUMAIUVHABUU
      MEUUMNZWOABYOUUMUUMEUUIUVIWTWPRYAXTUUMXQIUVHUVGWQXNXTVLYAXOXQUUMUUKXNUUMX
      OIXTBXOUUMYBUVIWRRWSXQYOCYLUUMUUBUVFUUCXASXBRZXCXDUULYFUURYFUUOUQJZPZUUMU
      UPUULYJUUTYFUVLUUPTUVAUVDAUUPUUOUVKYFUURABUUOUUONZEXEUUPNZUVKNZWKSUULYFUU
      RYOPZYLUVLUUMXTYHUVPYLKXNXQYOCUUQYLYFUUBUVFUUCUVCXFWJUULUUTYJUVPUVLKUVDUV
      AABYOUVKUUOUURYFEUUIUVMUVOXGSUVJXHXDUUNBUUOXOUUPUUMYFYBUVIUVEUVMUVNXJWAWB
      WCXKXIXLXM $.
      $( [2-Dec-2014] $)
  $}

  ${
    $d x y K $.  $d x y L $.
    drngprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    drngprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    drngprop.m $e |- ( .r ` K ) = ( .r ` L ) $.
    $( If two structures have the same ring components (properties), one is a
       division ring iff the other one is. $)
    drngprop $p |- ( K e. DivRing <-> L e. DivRing ) $=
      ( vx vy crg wcel cui cfv cbs c0g csn cdif wceq wa a1i co eqid eqidd cmulr
      cdr cv oveqi unitpropd cplusg elex rngprop sylbi grpidpropd sneqd difeq2d
      cvv eqeq12d pm5.32i anbi1i bitri isdrng 3bitr4i ) AHIZAJKZALKZAMKZNZOZPZQ
      ZBHIZBJKZVCBMKZNZOZPZQZAUCIBUCIVHVAVNQVOVAVGVNVAVBVJVFVMVAFGVCABVAVCUAZVC
      BLKPVACRZFUDZGUDZAUBKZSVRVSBUBKZSPVAVRVCIVSVCIQQZVTWAVRVSEUERUFVAVEVLVCVA
      VDVKVAFGVCABVPVQVRVSAUGKZSVRVSBUGKZSPWBWCWDVRVSDUERAHUHVAVIBUNIABCDEUIZBH
      UHUJUKULUMUOUPVAVIVNWEUQURVCAVBVDVCTVBTVDTUSVCBVJVKCVJTVKTUSUT $.
      $( [28-Dec-2014] $) $( [11-Oct-2013] $)
  $}

  ${
    drngmgp.b $e |- B = ( Base ` R ) $.
    drngmgp.z $e |- Z = ( 0g ` R ) $.
    drngmgp.g $e |- G = ( ( mulGrp ` R ) |`s ( B \ { Z } ) ) $.
    $( A division ring contains a multiplicative group. $)
    drngmgp $p |- ( R e. DivRing -> G e. Grp ) $=
      ( cdr wcel crg cgrp isdrng2 simprbi ) BHIBJICKIABCDEFGLM $.
      $( [8-Sep-2011] $)
  $}

  ${
    drngmcl.b $e |- B = ( Base ` R ) $.
    drngmcl.t $e |- T = ( .r ` R ) $.
    drngmcl.z $e |- Z = ( 0g ` R ) $.
    $( The product of two nonzero elements of a division ring is nonzero. $)
    drngmcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } )
                    /\ Y e. ( B \ { Z } ) ) -> ( X T Y ) e. ( B \ { Z } ) ) $=
      ( cdr wcel cmgp cfv csn cdif cress co cgrp eqid drngmgp wceq difss mgpbas
      wss cbs ressbas2 ax-mp mgpplusg ressplusg grpcl syl3an1 ) BJKBLMZAFNZOZPQ
      ZRKDUNKEUNKDECQUNKABUOFGIUOSZTUNCUODEUNAUDUNUOUEMUAAUMUBUNAUOULUPABULULSZ
      GUCUFUGUNCULUOUPBCULUQHUHUIUJUK $.
      $( [7-Sep-2011] $)
  $}

  ${
    drngid.b $e |- B = ( Base ` R ) $.
    drngid.z $e |- Z = ( 0g ` R ) $.
    drngid.u $e |- U = ( 1r ` R ) $.
    drngid.g $e |- G = ( ( mulGrp ` R ) |`s ( B \ { Z } ) ) $.
    $( A division ring's unit is the identity element of its multiplicative
       group. $)
    drngid $p |- ( R e. DivRing -> U = ( 0g ` G ) ) $=
      ( cdr wcel cmgp cfv cui cress co c0g crg wceq eqid drngrng unitgrpid cdif
      syl csn isdrng simprbi oveq2d syl6eqr fveq2d eqtrd ) BJKZCBLMZBNMZOPZQMZD
      QMULBRKZCUPSBUABUNUOCUNTZUOTHUBUDULUODQULUOUMAEUEUCZOPDULUNUSUMOULUQUNUSS
      ABUNEFURGUFUGUHIUIUJUK $.
      $( [7-Sep-2011] $)
  $}

  ${
    drngunz.z $e |- Z = ( 0g ` R ) $.
    drngunz.u $e |- U = ( 1r ` R ) $.
    $( A division ring's unit is different from its zero. $)
    drngunz $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cdr wcel cbs cfv wne cui wa crg drngrng 1unit syl drngunit mpbid simprd
      eqid ) AFGZBAHIZGZBCJZUABAKIZGZUCUDLUAAMGUFANAUEBUETZEOPUBAUEBCUBTUGDQRS
      $.
      $( [8-Sep-2011] $)
  $}

  ${
    drngid2.b $e |- B = ( Base ` R ) $.
    drngid2.t $e |- T = ( .r ` R ) $.
    drngid2.o $e |- Z = ( 0g ` R ) $.
    drngid2.u $e |- U = ( 1r ` R ) $.
    $( Properties showing that an element ` I ` is the identity element of a
       division ring. $)
    drngid2 $p |- ( R e. DivRing
        -> ( ( I e. B /\ I =/= Z /\ ( I T I ) = I ) <-> U = I ) ) $=
      ( cdr wcel wne co wceq w3a cmgp cfv wa eqid csn cdif cress df-3an eldifsn
      c0g anbi1i bitr4i wb drngmgp wss cbs difss mgpbas ressbas2 ax-mp mgpplusg
      cgrp ressplusg isgrpid2 syl syl5bb drngid eqeq1d bitr4d ) BKLZEALZEFMZEEC
      NEOZPZBQRZAFUAZUBZUCNZUFRZEOZDEOVJEVMLZVISZVFVPVJVGVHSZVISVRVGVHVIUDVQVSV
      IEAFUEUGUHVFVNURLVRVPUIABVNFGIVNTZUJVMCVNVOEVMAUKVMVNULROAVLUMVMAVNVKVTAB
      VKVKTZGUNUOUPVMCVKVNVTBCVKWAHUQUSVOTUTVAVBVFDVOEABDVNFGIJVTVCVDVE $.
      $( [11-Oct-2013] $)
  $}

  ${
    invrcl.b $e |- B = ( Base ` R ) $.
    invrcl.z $e |- Z = ( 0g ` R ) $.
    invrcl.i $e |- I = ( invr ` R ) $.
    $( Closure of the multiplicative inverse in a division ring.  ( ~ reccl
       analog.) $)
    drnginvrcl $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( I ` X ) e. B ) $=
      ( cdr wcel wne cfv wa cui eqid drngunit crg wi drngrng rnginvcl ex 3impib
      syl sylbird ) BIJZDAJZDEKZDCLAJZUEUFUGMDBNLZJZUHABUIDEFUIOZGPUEBQJZUJUHRB
      SULUJUHABUICDUKHFTUAUCUDUB $.
      $( [19-Apr-2014] $)

    $( The multiplicative inverse in a division ring is nonzero.  ( ~ recne0
       analog.) $)
    drnginvrn0 $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( I ` X ) =/= Z ) $=
      ( cdr wcel wne w3a cfv wa cui crg wi drngrng eqid drngunit ex syl 3imtr3d
      unitinvcl 3impib simprd ) BIJZDAJZDEKZLDCMZAJZUJEKZUGUHUIUKULNZUGDBOMZJZU
      JUNJZUHUINUMUGBPJZUOUPQBRUQUOUPBUNCDUNSZHUDUAUBABUNDEFURGTABUNUJEFURGTUCU
      EUF $.
      $( [19-Apr-2014] $)
  $}

  ${
    drnginvrl.b $e |- B = ( Base ` R ) $.
    drnginvrl.z $e |- Z = ( 0g ` R ) $.
    drnginvrl.t $e |- T = ( .r ` R ) $.
    drnginvrl.u $e |- U = ( 1r ` R ) $.
    drnginvrl.i $e |- I = ( invr ` R ) $.
    $( Property of the multiplicative inverse in a division ring.  ( ~ recid2
       analog.) $)
    drnginvrl $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( ( I ` X ) T X ) = U ) $=
      ( cdr wcel wne cfv co wceq wa cui drngunit crg wi drngrng unitlinv ex syl
      eqid sylbird 3impib ) BMNZFANZFGOZFEPFCQDRZUKULUMSFBTPZNZUNABUOFGHUOUHZIU
      AUKBUBNZUPUNUCBUDURUPUNBCUOEDFUQLJKUEUFUGUIUJ $.
      $( [19-Apr-2014] $)

    $( Property of the multiplicative inverse in a division ring.  ( ~ recid
       analog.) $)
    drnginvrr $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( X T ( I ` X ) ) = U ) $=
      ( cdr wcel wne cfv co wceq wa cui drngunit crg wi drngrng unitrinv ex syl
      eqid sylbird 3impib ) BMNZFANZFGOZFFEPCQDRZUKULUMSFBTPZNZUNABUOFGHUOUHZIU
      AUKBUBNZUPUNUCBUDURUPUNBCUOEDFUQLJKUEUFUGUIUJ $.
      $( [19-Apr-2014] $)
  $}

  ${
    drngmuleq0.b $e |- B = ( Base ` R ) $.
    drngmuleq0.o $e |- O = ( 0g ` R ) $.
    drngmuleq0.t $e |- T = ( .r ` R ) $.
    drngmuleq0.r $e |- ( ph -> R e. DivRing ) $.
    drngmuleq0.x $e |- ( ph -> X e. B ) $.
    drngmuleq0.y $e |- ( ph -> Y e. B ) $.
    $( A product is zero iff one of its factors is zero. $)
    drngmul0or $p |- ( ph -> ( ( X T Y ) = O <-> ( X = O \/ Y = O ) ) ) $=
      ( co wceq wa cfv wcel adantr syl2anc wo wn wne df-ne cinvr oveq2 ad2antlr
      cur cdr simpr eqid drnginvrl syl3anc oveq1d crg drngrng drnginvrcl rngass
      syl13anc rnglidm 3eqtr3d adantlr rngrz ex syl5bir orrd rnglz oveq1 eqeq1d
      syl syl5ibrcom jaod impbid ) AFGDNZEOZFEOZGEOZUAZAVOVRAVOPZVPVQVPUBFEUCZV
      SVQFEUDVSVTVQVSVTPZFCUEQZQZVNDNZWCEDNZGEVOWDWEOAVTVNEWCDUFUGAVTWDGOVOAVTP
      ZWCFDNZGDNZCUHQZGDNZWDGWFWGWIGDWFCUIRZFBRZVTWGWIOAWKVTKSZAWLVTLSZAVTUJZBC
      DWIWBFEHIJWIUKZWBUKZULUMUNWFCUORZWCBRZWLGBRZWHWDOAWRVTAWKWRKCUPVJZSWFWKWL
      VTWSWMWNWOBCWBFEHIWQUQUMZWNAWTVTMSBCDWCFGHJURUSAWJGOZVTAWRWTXCXAMBCDWIGHJ
      WPUTTSVAVBWAWRWSWEEOVSWRVTAWRVOXASSAVTWSVOXBVBBCDWCEHJIVCTVAVDVEVFVDAVPVO
      VQAVOVPEGDNZEOZAWRWTXEXAMBCDGEHJIVGTVPVNXDEFEGDVHVIVKAVOVQFEDNZEOZAWRWLXG
      XALBCDFEHJIVCTVQVNXFEGEFDUFVIVKVLVM $.
      $( [8-Oct-2014] $)

    $( A product is nonzero iff both its factors are nonzero. $)
    drngmulne0 $p |- ( ph
     -> ( ( X T Y ) =/= O <-> ( X =/= O /\ Y =/= O ) ) ) $=
      ( co wne wceq wo wn wa drngmul0or necon3abid neanior syl6bbr ) AFGDNZEOFE
      PGEPQZRFEOGEOSAUEUDEABCDEFGHIJKLMTUAFEGEUBUC $.
      $( [18-Oct-2014] $)

    drngmuleq0.e $e |- ( ph -> Y =/= O ) $.
    $( An element is zero iff its product with a nonzero element is zero. $)
    drngmuleq0 $p |- ( ph -> ( ( X T Y ) = O <-> X = O ) ) $=
      ( co wceq wo drngmul0or wne wb wn df-ne orel2 orc impbid1 sylbi syl bitrd
      ) AFGDOEPFEPZGEPZQZUIABCDEFGHIJKLMRAGESZUKUITZNULUJUAZUMGEUBUNUKUIUJUIUCU
      IUJUDUEUFUGUH $.
      $( [8-Oct-2014] $)
  $}

  ${
    $d s x y z R $.  $d s E $.  $d x y z O $.  $d x y V $.
    opprdrng.1 $e |- O = ( oppR ` R ) $.
    $( The opposite of a division ring is also a division ring. $)
    opprdrng $p |- ( R e. DivRing -> O e. DivRing ) $=
      ( crg wcel cui cfv cbs c0g csn cdif wceq wa cdr opprrng adantr simpr eqid
      oppr0 isdrng sneqd difeq2d eqtrd jca opprbas opprunit 3imtr4i ) ADEZAFGZA
      HGZAIGZJZKZLZMZBDEZUIUJBIGZJZKZLZMANEBNEUOUPUTUHUPUNABCOPUOUIUMUSUHUNQUOU
      LURUJUOUKUQUHUKUQLUNABDUKCUKRZSPUAUBUCUDUJAUIUKUJRZUIRZVATUJBUIUQUJABCVBU
      EABUIVCCUFUQRTUG $.
      $( [18-Oct-2014] $)
  $}

  ${
    $d x y z B $.  $d x y z ph $.  $d x y z R $.  $d x y z T $.  $d x y z U $.
    $d y z I $.  $d x y z Z $.
    isdrngd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isdrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isdrngd.z $e |- ( ph -> Z = ( 0g ` R ) ) $.
    isdrngd.u $e |- ( ph -> U = ( 1r ` R ) ) $.
    isdrngd.r $e |- ( ph -> R e. Ring ) $.
    isdrngd.n $e |- ( ( ph /\ ( x e. B /\ x =/= Z )
            /\ ( y e. B /\ y =/= Z ) ) -> ( x T y ) =/= Z ) $.
    isdrngd.o $e |- ( ph -> U =/= Z ) $.
    isdrngd.i $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I e. B ) $.
    isdrngd.j $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I =/= Z ) $.
    ${
      isdrngd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( I T x ) = U ) $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ." $)
      isdrngd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz crg cmgp cfv cbs c0g csn cdif cress co cgrp wa cdr wceq difss
        wss syl5sseq mgpbas ressbas2 syl cmulr cplusg mgpplusg ressplusg syl6eq
        eqid cv wne eldifsn rngcl syl3an1 3expib eleq2d anbi12d eleq12d 3imtr4d
        w3a oveqd 3impib 3adant2r 3adant3r sylanbrc syl3an3b syl3an2b 3anim123i
        eldifi wi rngass ex 3anbi123d eqidd oveq123d eqeq12d imp sylan2 rngidcl
        cur mpbird rnglidm eqeq1d adantrr sylan2b isgrpd difeq12d oveq2d eleq1d
        sneqd anbi2d mpbi2and isdrng2 sylibr ) AEUBTZEUCUDZEUEUDZEUFUDZUGZUHZUI
        UJZUKTZULZEUMTAXLXMDIUGZUHZUIUJZUKTZXTNABCUAYBFYCHGAYBXNUPYBYCUEUDUNADY
        BXNDYAUOJUQYBXNYCXMYCVFZXNEXMXMVFZXNVFZURUSUTAFEVAUDZYCVBUDKYBYHXMYCYEE
        YHXMYFYHVFZVCVDVEBVGZYBTZAYJDTZYJIVHZULZCVGZYBTZYJYOFUJZYBTZYJDIVIZYPAY
        NYODTZYOIVHZULZYRYODIVIAYNUUBVQYQDTZYQIVHYRAYNYTUUCUUAAYLYTUUCYMAYLYTUU
        CAYJXNTZYOXNTZULYJYOYHUJZXNTZYLYTULUUCAUUDUUEUUGAXLUUDUUEUUGNXNEYHYJYOY
        GYIVJVKVLAYLUUDYTUUEADXNYJJVMZADXNYOJVMZVNAYQUUFDXNAFYHYJYOKVRZJVOVPVSV
        TWAOYQDIVIWBWCWDYKYPUAVGZYBTZVQAYLYTUUKDTZVQZYQUUKFUJZYJYOUUKFUJZFUJZUN
        ZYKYLYPYTUULUUMYJDYAWFYODYAWFUUKDYAWFWEAUUNUURAUUDUUEUUKXNTZVQZUUFUUKYH
        UJZYJYOUUKYHUJZYHUJZUNZUUNUURAXLUUTUVDWGNXLUUTUVDXNEYHYJYOUUKYGYIWHWIUT
        AYLUUDYTUUEUUMUUSUUHUUIADXNUUKJVMWJAUUOUVAUUQUVCAYQUUFUUKUUKFYHKUUJAUUK
        WKWLAYJYJUUPUVBFYHKAYJWKZAFYHYOUUKKVRWLWMVPWNWOAGDTZGIVHGYBTAUVFEWQUDZX
        NTZAXLUVHNXNEUVGYGUVGVFZWPUTAGUVGDXNMJVOWRPGDIVIWBYKAYNGYJFUJZYJUNZYSAY
        LUVKYMAYLUVKAUUDUVGYJYHUJZYJUNZYLUVKAXLUUDUVMWGNXLUUDUVMXNEYHUVGYJYGYIU
        VIWSWIUTUUHAUVJUVLYJAGUVGYJYJFYHKMUVEWLWTVPWNXAXBYKAYNHYBTZYSAYNULHDTHI
        VHUVNQRHDIVIWBXBYKAYNHYJFUJGUNYSSXBXCAYDXSXLAYCXRUKAYBXQXMUIADXNYAXPJAI
        XOLXGXDXEXFXHXIXNEXRXOYGXOVFXRVFXJXK $.
        $( [2-Aug-2013] $)
    $}

    ${
      isdrngrd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( x T I ) = U ) $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ."  This
         version of ~ isdrngd requires a right reciprocal instead of left. $)
      isdrngrd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz crg cui cfv cbs c0g csn cdif wceq cdr coppr cmulr eqid syl6eq
        opprbas eqidd oppr0 syl eqtrd cur oppr1 opprrng cv wa co wi eleq1 neeq1
        wne w3a anbi12d 3anbi2d oveq1 neeq1d imbi12d 3anbi3d oveq2 oveqd simp3l
        3ad2ant1 eleqtrd simp2l opprmul syl2anc eqnetrrd 3com23 chvarv 3eqtr3rd
        adantr simprl isdrngd opprunit isdrng simprbi difeq2d eqtr4d sylanbrc
        sneqd ) AEUBTZEUCUDZEUEUDZEUFUDZUGZUHZUIEUJTNAWTXAEUKUDZUFUDZUGZUHZXDAX
        EUJTZWTXHUIZABUADXEXEULUDZGHIADXAXEUEUDJXAEXEXEUMZXAUMZUOZUNAXKUPAIXBXF
        LAWSXBXFUINEXEUBXBXLXBUMZUQURZUSAGEUTUDZXEUTUDZMAWSXQXRUINEXQXEUBXLXQUM
        VAURUSAWSXEUBTZNEXEXLVBURACVCZDTZXTIVIZVDZUAVCZDTZYDIVIZVDZVJZXTYDXKVEZ
        IVIZVFZABVCZDTZYLIVIZVDZYGVJZYLYDXKVEZIVIZVFCBXTYLUIZYHYPYJYRYSYCYOAYGY
        SYAYMYBYNXTYLDVGXTYLIVHVKVLYSYIYQIXTYLYDXKVMVNVOAYCYOVJZXTYLXKVEZIVIZVF
        YKBUAYLYDUIZYTYHUUBYJUUCYOYGAYCUUCYMYEYNYFYLYDDVGYLYDIVHVKVPUUCUUAYIIYL
        YDXTXKVQVNVOAYOYCUUBAYOYCVJZYLXTFVEZUUAIUUDUUEYLXTEULUDZVEZUUAUUDFUUFYL
        XTAYOFUUFUIZYCKVTVRUUDXTXATYLXATZUUGUUAUIUUDXTDXAAYOYAYBVSAYODXAUIZYCJV
        TZWAUUDYLDXAAYMYNYCWBUUKWAXAEUUFXKXEXTYLXMUUFUMZXLXKUMZWCWDUSOWEWFWGWGP
        QRAYOVDZYLHFVEYLHUUFVEZGHYLXKVEZUUNFUUFYLHAUUHYOKWIVRSUUNHXATUUIUUOUUPU
        IUUNHDXAQAUUJYOJWIZWAUUNYLDXAAYMYNWJUUQWAXAEUUFXKXEHYLXMUULXLUUMWCWDWHW
        KXIXSXJXAXEWTXFXNEXEWTWTUMZXLWLXFUMWMWNURAXCXGXAAXBXFXPWRWOWPXAEWTXBXMU
        URXOWMWQ $.
        $( [10-Aug-2013] $)
    $}
  $}

