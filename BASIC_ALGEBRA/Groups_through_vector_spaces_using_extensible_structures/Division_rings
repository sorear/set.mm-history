$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Division rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c DivRing $.

  $( Extend class notation with class of all division rings. $)
  cdr $a class DivRing $.

  $( Define class of all division rings.  A division ring is a ring in which
     the set of units is exactly the nonzero elements of the ring. $)
  df-drng $a |- DivRing = { r e. Ring |
    ( Unit ` r ) = ( ( Base ` r ) \ { ( 0g ` r ) } ) } $.

  ${
    $d r B $.  $d r R $.  $d r U $.  $d r Z $.
    isdrng.b $e |- B = ( Base ` R ) $.
    isdrng.u $e |- U = ( Unit ` R ) $.
    isdrng.z $e |- Z = ( 0g ` R ) $.
    $( The predicate "is a division ring".  (Revised by Mario Carneiro,
       2-Dec-2014.) $)
    isdrng $p |- ( R e. DivRing <-> ( R e. Ring /\ U = ( B \ { Z } ) ) ) $=
      ( vr cv cui cfv cbs c0g csn cdif wceq crg cdr fveq2 syl6eqr sneqd eqeq12d
      difeq12d df-drng elrab2 ) HIZJKZUFLKZUFMKZNZOZPCADNZOZPHBQRUFBPZUGCUKUMUN
      UGBJKCUFBJSFTUNUHAUJULUNUHBLKAUFBLSETUNUIDUNUIBMKDUFBMSGTUAUCUBHUDUE $.
      $( [2-Dec-2014] $) $( [18-Oct-2012] $)

    $( Elementhood in the set of units when ` R ` is a division ring. $)
    drngunit $p |- ( R e. DivRing -> ( X e. U <-> ( X e. B /\ X =/= Z ) ) ) $=
      ( cdr wcel csn cdif wne wa crg wceq isdrng simprbi eleq2d eldifsn syl6bb
      ) BIJZDCJDAEKLZJDAJDEMNUBCUCDUBBOJCUCPABCEFGHQRSDAETUA $.
      $( [2-Dec-2014] $)
  $}

  $( A division ring is a ring. $)
  drngrng $p |- ( R e. DivRing -> R e. Ring ) $=
    ( cdr wcel crg cui cfv cbs c0g csn cdif wceq eqid isdrng simplbi ) ABCADCAE
    FZAGFZAHFZIJKPAOQPLOLQLMN $.
    $( [8-Sep-2011] $)

  $( A division ring is a group. $)
  drnggrp $p |- ( R e. DivRing -> R e. Grp ) $=
    ( cdr wcel crg cgrp drngrng rnggrp syl ) ABCADCAECAFAGH $.
    $( [8-Sep-2011] $)

  ${
    $d x y B $.  $d x y G $.  $d x y R $.  $d y T $.  $d x Z $.
    isdrng2.b $e |- B = ( Base ` R ) $.
    isdrng2.u $e |- T = ( .r ` R ) $.
    isdrng2.z $e |- Z = ( 0g ` R ) $.
    isdrng2.g $e |- G =
      { <. ( Base ` ndx ) , ( B \ { Z } ) >. , <. ( +g ` ndx ) , T >. } $.
    $( A division ring can equivalently be defined as a ring such that the
       nonzero elements form a group under multiplication (from which it
       follows that this is the same group as the group of units).
       (Contributed by Mario Carneiro, 2-Dec-2014.) $)
    isdrng2 $p |- ( R e. DivRing <-> ( R e. Ring /\ G e. Grp ) ) $=
      ( vx vy wcel cfv wceq wa eqid cbs adantr co cvv cdr crg cui csn cdif cgrp
      isdrng cnx cop cplusg cpr simpr opeq2d preq1d syl6eqr unitgrp eqeltrrd cv
      wne unitcl adantlr c0g cdvr eqeltri difexg ax-mp grpbase grpidcl ad2antlr
      fvex eldifsn sylib simprd simpll simpld rngdvcan1 rngdvcl syl2anc 3netr4d
      rngrz oveq2 necon3i syl sylanbrc ex ssrdv cur wrex isunit ad2antrr eldifi
      wb adantl cminusg grpinvcl adantll cmulr grpplusg grprinv rngidcl rnglidm
      mpdan 1unit sseldd grpid mpbid eqtrd grplinv eqeq1d oveq1 anbi12d rcla4ev
      syl12anc mpbir2and eqssd impbida pm5.32i bitri ) BUALBUBLZBUCMZAEUDZUEZNZ
      OZXSDUFLZOZABXTEFXTPZHUGXSYCYEXSYCYEYDUHQMZXTUIZUHUJMCUIZUKZDUFYDYKYHYBUI
      ZYJUKDYDYIYLYJYDXTYBYHXSYCULUMUNIUOXSYKUFLYCBCXTYKYGGYKPUPRUQYFXTYBYFJXTY
      BYFJURZXTLZYMYBLZYFYNOZYMALZYMEUSZYOXSYNYQYEABXTUBYMFYGUTVAYPDVBMZYMBVCMZ
      SZYMCSZUUAECSZUSYRYPYSEUUBUUCYPYSALZYSEUSZYPYSYBLZUUDUUEOYEUUFXSYNYBDYSYB
      TLZYBDQMNATLUUGABQMTFBQVJVDAYATVEVFYBCDTIVGVFZYSPZVHVIYSAEVKVLZVMYPAYTBCX
      TYSYMFYGYTPZXSYEYNVNZYPUUDUUEUUJVOZYFYNULZGVPYPXSUUAALUUCENUULYPAYTBXTYSY
      MFYGUUKUULUUMUUNVQABCUUAEFGHVTVRVSYMEUUBUUCYMEUUACWAWBWCYMAEVKWDWEWFZYFJY
      BXTYFYOYNYFYOOZYNYQYMKURZCSZBWGMZNZUUQYMCSZUUSNZOZKAWHZXSYNYQUVDOWLYEYOKA
      BCXTUUSUBYMYGUUSPZFGWIWJYOYQYFYMAYAWKWMUUPYMDWNMZMZALZYMUVGCSZUUSNZUVGYMC
      SZUUSNZUVDUUPUVGYBLZUVHYEYOUVMXSYBDUVFYMUUHUVFPZWOWPUVGAYAWKWCUUPUVIYSUUS
      YEYOUVIYSNXSYBCDUVFYSYMUUHCTLCDUJMNCBWQMTGBWQVJVDYBCDTIWRVFZUUIUVNWSWPYFY
      SUUSNZYOYFUUSUUSCSUUSNZUVPXSUVQYEXSUUSALUVQABUUSFUVEWTABCUUSUUSFGUVEXAXBR
      YFYEUUSYBLUVQUVPWLXSYEULYFXTYBUUSUUOXSUUSXTLYEBXTUUSYGUVEXCRXDYBCDYSUUSUU
      HUVOUUIXEVRXFRZXGUUPUVKYSUUSYEYOUVKYSNXSYBCDUVFYSYMUUHUVOUUIUVNXHWPUVRXGU
      VCUVJUVLOKUVGAUUQUVGNZUUTUVJUVBUVLUVSUURUVIUUSUUQUVGYMCWAXIUVSUVAUVKUUSUU
      QUVGYMCXJXIXKXLXMXNWEWFXOXPXQXR $.
      $( [2-Dec-2014] $)
  $}

  ${
    $d u x K $.  $d u x L $.
    dvrngprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    dvrngprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    dvrngprop.m $e |- ( .r ` K ) = ( .r ` L ) $.
    $( If two structures have the same ring components (properties), one is a
       division ring iff the other one is. $)
    drngprop $p |- ( K e. DivRing <-> L e. DivRing ) $=
      ( vu vx cvv wcel cdr wn cbs cfv c0 wceq cgrp syl cop wa eqid wb con3i wne
      elex fvprc drnggrp grpn0 necon2bi 2falsed eqcomi crg cnx c0g cplusg cmulr
      csn cdif cpr isdrng2 cv co wral crio grpidval adantr adantl sneqd difeq2d
      eqtr4d opeq2d preq1d eleq1d anbi2d rngprop anbi1i 3bitr4g syl5bb ecase )
      AHIZBHIZAJIZBJIZUAVSKZWAWBWAVSAJUDUBWCALMZNOWBKALUEWBWDNWBBPIWDNUCBUFWDBC
      UGQUHQUIVTKZWAWBWEBLMZNOWAKBLUEWAWFNWAAPIWFNUCAUFWFAWDWFCUJUGQUHQWBVTBJUD
      UBUIWAAUKIZULLMZWDAUMMZUPZUQZRZULUNMAUOMZRZURZPIZSZVSVTSZWBWDAWMWOWIWDTZW
      MTWITZWOTUSWRBUKIZWPSXAWHWDBUMMZUPZUQZRZWNURZPIZSWQWBWRWPXGXAWRWOXFPWRWLX
      EWNWRWKXDWHWRWJXCWDWRWIXBWRWIFUTGUTZAUNMZVAXHOGWDVBFWDVCZXBVSWIXJOVTGWDXI
      FAWIHWSXITWTVDVEVTXBXJOVSGWDXIFBXBHCDXBTZVDVFVIVGVHVJVKVLVMWGXAWPABCDEVNV
      OWDBWMXFXBCEXKXFTUSVPVQVR $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z T $.  $d x U $.  $d x y z Z $.  $d x y z ph $.
    isdivrngd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isdivrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isdivrngd.z $e |- ( ph -> Z = ( 0g ` R ) ) $.
    isdivrngd.u $e |- ( ph -> U = ( 1r ` R ) ) $.
    isdivrngd.r $e |- ( ph -> R e. Ring ) $.
    isdivrngd.n $e |- ( ( ph /\ ( x e. B /\ x =/= Z )
            /\ ( y e. B /\ y =/= Z ) ) -> ( x T y ) =/= Z ) $.
    isdivrngd.o $e |- ( ph -> U =/= Z ) $.
    isdivrngd.i $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I e. B ) $.
    isdivrngd.j $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I =/= Z ) $.
    ${
      isdivrngd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( I T x ) = U ) $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ." $)
      isdrngd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz crg cnx cbs cfv c0g csn cdif cop cplusg cmulr cpr cgrp wa cdr
        sneqd difeq12d opeq2d preq12d eleq1d anbi2d cvv wceq fvex syl6eqel eqid
        difexg grpbase 3syl grpplusg syl prex a1i cv wne co eldifsn w3a syl3an1
        3expib eleq2d anbi12d eleq12d 3imtr4d 3impib 3adant2r 3adant3r sylanbrc
        rngcl oveqd syl3an3b syl3an2b eldifi 3anim123i wi ex 3anbi123d oveq123d
        rngass eqeq12d imp sylan2 rngidcl mpbird rnglidm eqeq1d adantrr sylan2b
        eqidd cur isgrpd mpbi2and isdrng2 sylibr ) AEUBTZUCUDUEZEUDUEZEUFUEZUGZ
        UHZUIZUCUJUEZEUKUEZUIZULZUMTZUNZEUOTAXOXPDIUGZUHZUIZYBFUIZULZUMTZYGAYMY
        FXOAYLYEUMAYJYAYKYDAYIXTXPADXQYHXSJAIXRLUPUQURAFYCYBKURUSUTVANABCUAYIFY
        LHGADVBTYIVBTYIYLUDUEVCADXQVBJEUDVDVEDYHVBVGYIFYLVBYLVFZVHVIAFVBTFYLUJU
        EVCAFYCVBKEUKVDVEYIFYLVBYNVJVKYLVBTAYJYKVLVMBVNZYITZAYODTZYOIVOZUNZCVNZ
        YITZYOYTFVPZYITZYODIVQZUUAAYSYTDTZYTIVOZUNZUUCYTDIVQAYSUUGVRUUBDTZUUBIV
        OUUCAYSUUEUUHUUFAYQUUEUUHYRAYQUUEUUHAYOXQTZYTXQTZUNYOYTYCVPZXQTZYQUUEUN
        UUHAUUIUUJUULAXOUUIUUJUULNXQEYCYOYTXQVFZYCVFZWIVSVTAYQUUIUUEUUJADXQYOJW
        AZADXQYTJWAZWBAUUBUUKDXQAFYCYOYTKWJZJWCWDWEWFWGOUUBDIVQWHWKWLYPUUAUAVNZ
        YITZVRAYQUUEUURDTZVRZUUBUURFVPZYOYTUURFVPZFVPZVCZYPYQUUAUUEUUSUUTYODYHW
        MYTDYHWMUURDYHWMWNAUVAUVEAUUIUUJUURXQTZVRZUUKUURYCVPZYOYTUURYCVPZYCVPZV
        CZUVAUVEAXOUVGUVKWONXOUVGUVKXQEYCYOYTUURUUMUUNWSWPVKAYQUUIUUEUUJUUTUVFU
        UOUUPADXQUURJWAWQAUVBUVHUVDUVJAUUBUUKUURUURFYCKUUQAUURXIWRAYOYOUVCUVIFY
        CKAYOXIZAFYCYTUURKWJWRWTWDXAXBAGDTZGIVOGYITAUVMEXJUEZXQTZAXOUVONXQEUVNU
        UMUVNVFZXCVKAGUVNDXQMJWCXDPGDIVQWHYPAYSGYOFVPZYOVCZUUDAYQUVRYRAYQUVRAUU
        IUVNYOYCVPZYOVCZYQUVRAXOUUIUVTWONXOUUIUVTXQEYCUVNYOUUMUUNUVPXEWPVKUUOAU
        VQUVSYOAGUVNYOYOFYCKMUVLWRXFWDXAXGXHYPAYSHYITZUUDAYSUNHDTHIVOUWAQRHDIVQ
        WHXHYPAYSHYOFVPGVCUUDSXHXKXLXQEYCYEXRUUMUUNXRVFYEVFXMXN $.
        $( [2-Aug-2013] $)
    $}

    ${
      $d y z I $.
      isdivrngrd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( x T I ) = U )
          $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ."  In this
         version of ~ isdrngd we are use right reciprocal of ~ isgrprd rather
         than the left reciprocal of ~ isgrpd . $)
      isdrngrd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz cv wne wa csn cdif co wceq eldifsn w3a cbs cfv cmulr crg eqid
        rngcl syl3an1 3expib anbi12d eleq12d 3imtr4d 3adant2r 3adant3r sylanbrc
        eleq2d 3impib syl3an3b syl3an2b cur rngidcl syl eleqtrrd eqeltrd eldifi
        oveqd eqidd oveq123d adantr rngridm sylan ex sylbid imp eqtrd 3anim123i
        sylan2 3anbi123d wi rngass 3eqtr4d sylan2b grplinvd sylan2br isdrngd )
        ABCDEFGHIJKLMNOPQRBUBZDTZWOIUCZUDZAWODIUEZUFZTZHWOFUGGUHWODIUIZABCUAWTF
        HGXAAWRCUBZWTTZWOXCFUGZWTTZXBXDAWRXCDTZXCIUCZUDZXFXCDIUIAWRXIUJXEDTZXEI
        UCXFAWRXGXJXHAWPXGXJWQAWPXGXJAWOEUKULZTZXCXKTZUDWOXCEUMULZUGZXKTZWPXGUD
        XJAXLXMXPAEUNTZXLXMXPNXKEXNWOXCXKUOZXNUOZUPUQURAWPXLXGXMADXKWOJVEZADXKX
        CJVEZUSAXEXODXKAFXNWOXCKVOZJUTVAVFVBVCOXEDIUIVDVGVHAGDTGIUCGWTTAGEVIULZ
        DMAYCXKDAXQYCXKTNXKEYCXRYCUOZVJVKJVLVMPGDIUIVDXAAWPWOGFUGZWOUHWODWSVNZA
        WPUDYEWOYCXNUGZWOAYEYGUHWPAWOWOGYCFXNKAWOVPZMVQVRAWPYGWOUHZAWPXLYIXTAXL
        YIAXQXLYINXKEXNYCWOXRXSYDVSVTWAWBWCWDWFXAXDUAUBZWTTZUJAWPXGYJDTZUJZXEYJ
        FUGZWOXCYJFUGZFUGZUHXAWPXDXGYKYLYFXCDWSVNYJDWSVNWEAYMUDXOYJXNUGZWOXCYJX
        NUGZXNUGZYNYPAYMYQYSUHZAYMXLXMYJXKTZUJZYTAWPXLXGXMYLUUAXTYAADXKYJJVEWGA
        XQUUBYTWHNXQUUBYTXKEXNWOXCYJXRXSWIWAVKWBWCAYNYQUHYMAXEXOYJYJFXNKYBAYJVP
        VQVRAYPYSUHYMAWOWOYOYRFXNKYHAFXNXCYJKVOVQVRWJWFXAAWRHWTTZXBAWRUDHDTHIUC
        UUCQRHDIUIVDWKXAAWRWOHFUGGUHXBSWKWLWMWN $.
        $( [10-Aug-2013] $)
    $}
  $}

  ${
    divrngmgrp.b $e |- B = ( Base ` R ) $.
    divrngmgrp.t $e |- T = ( .r ` R ) $.
    divrngmgrp.z $e |- Z = ( 0g ` R ) $.
    divrngmgrp.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( A division ring contains a multiplicative group. $)
    drngmgrp $p |- ( R e. DivRing -> G e. Grp ) $=
      ( cdr wcel crg cgrp isdrng2 simprbi ) BJKBLKDMKABCDEFGHINO $.
      $( [8-Sep-2011] $)
  $}

  ${
    divrngmcl.b $e |- B = ( Base ` R ) $.
    divrngmcl.t $e |- T = ( .r ` R ) $.
    divrngmcl.z $e |- Z = ( 0g ` R ) $.
    $( The product of two nonzero elements of a division ring is nonzero. $)
    drngmcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } )
                    /\ Y e. ( B \ { Z } ) ) -> ( X T Y ) e. ( B \ { Z } ) ) $=
      ( wcel cnx cbs cfv cop cplusg cvv wceq fvex eqeltri ax-mp cdr csn cdif co
      cpr cgrp eqid drngmgrp difexg grpbase cmulr grpplusg grpcl syl3an1 ) BUAJ
      KLMAFUBZUCZNKOMCNUEZUFJDUPJEUPJDECUDUPJABCUQFGHIUQUGZUHUPCUQDEUPPJZUPUQLM
      QAPJUSABLMPGBLRSAUOPUITUPCUQPURUJTCPJCUQOMQCBUKMPHBUKRSUPCUQPURULTUMUN $.
      $( [7-Sep-2011] $)
  $}

  ${
    drngid.b $e |- B = ( Base ` R ) $.
    drngid.t $e |- T = ( .r ` R ) $.
    drngid.z $e |- Z = ( 0g ` R ) $.
    drngid.u $e |- U = ( 1r ` R ) $.
    drngid.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( A division ring's unit is the identity element of its multiplicative
       group. $)
    drngid $p |- ( R e. DivRing -> U = ( 0g ` G ) ) $=
      ( cdr wcel cnx cfv cop cpr c0g wceq eqid cbs cui cplusg drngrng unitgrpid
      crg syl csn cdif isdrng simprbi opeq2d preq1d syl6eqr fveq2d eqtrd ) BLMZ
      DNUAOZBUBOZPZNUCOCPZQZROZEROUQBUFMZDVCSBUDBCUSVBDUSTZHVBTJUEUGUQVBERUQVBU
      RAFUHUIZPZVAQEUQUTVGVAUQUSVFURUQVDUSVFSABUSFGVEIUJUKULUMKUNUOUP $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngunz.z $e |- Z = ( 0g ` R ) $.
    divrngunz.u $e |- U = ( 1r ` R ) $.
    $( A division ring's unit is different from its zero. $)
    drngunz $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cdr wcel cbs cfv wne cui wa crg drngrng 1unit syl drngunit mpbid simprd
      eqid ) AFGZBAHIZGZBCJZUABAKIZGZUCUDLUAAMGUFANAUEBUETZEOPUBAUEBCUBTUGDQRS
      $.
      $( [8-Sep-2011] $)
  $}

  ${
    dvrngid2.b $e |- B = ( Base ` R ) $.
    dvrngid2.t $e |- T = ( .r ` R ) $.
    dvrngid2.o $e |- Z = ( 0g ` R ) $.
    dvrngid2.u $e |- U = ( 1r ` R ) $.
    $( Properties showing that an element ` I ` is the identity element of a
       division ring. $)
    drngid2 $p |- ( R e. DivRing
        -> ( ( I e. B /\ I =/= Z /\ ( I T I ) = I ) <-> U = I ) ) $=
      ( wcel wceq cnx cbs cfv cop cplusg wa eqid cvv cdr wne co w3a csn cpr c0g
      cdif df-3an eldifsn anbi1i bitr4i cgrp wb drngmgrp eqeltri difexg grpbase
      fvex mp2b cmulr grpplusg ax-mp isgrpid2 syl syl5bb drngid eqeq1d bitr4d )
      BUAKZEAKZEFUBZEECUCELZUDZMNOAFUEZUHZPMQOCPUFZUGOZELZDELVNEVPKZVMRZVJVSVNV
      KVLRZVMRWAVKVLVMUIVTWBVMEAFUJUKULVJVQUMKWAVSUNABCVQFGHIVQSZUOVPCVQVREATKV
      PTKVPVQNOLABNOTGBNUSUPAVOTUQVPCVQTWCURUTCTKCVQQOLCBVAOTHBVAUSUPVPCVQTWCVB
      VCVRSVDVEVFVJDVREABCDVQFGHIJWCVGVHVI $.
      $( [11-Oct-2013] $)
  $}

  ${
    invrcl.b $e |- B = ( Base ` R ) $.
    invrcl.z $e |- Z = ( 0g ` R ) $.
    invrcl.i $e |- I = ( invr ` R ) $.
    $( Closure of the multiplicative inverse in a division ring.  ( ~ reccl
       analog.) $)
    drnginvrcl $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( I ` X ) e. B ) $=
      ( cdr wcel wne cfv wa cui eqid drngunit crg wi drngrng rnginvcl ex 3impib
      syl sylbird ) BIJZDAJZDEKZDCLAJZUEUFUGMDBNLZJZUHABUIDEFUIOZGPUEBQJZUJUHRB
      SULUJUHABUICDUKHFTUAUCUDUB $.
      $( [19-Apr-2014] $)

    $( The multiplicative inverse in a division ring is nonzero.  ( ~ recne0
       analog.) $)
    drnginvrn0 $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( I ` X ) =/= Z ) $=
      ( cdr wcel wne w3a cfv wa cui crg wi drngrng eqid drngunit ex syl 3imtr3d
      unitinvcl 3impib simprd ) BIJZDAJZDEKZLDCMZAJZUJEKZUGUHUIUKULNZUGDBOMZJZU
      JUNJZUHUINUMUGBPJZUOUPQBRUQUOUPBUNCDUNSZHUDUAUBABUNDEFURGTABUNUJEFURGTUCU
      EUF $.
      $( [19-Apr-2014] $)
  $}

  ${
    invrl.b $e |- B = ( Base ` R ) $.
    invrl.z $e |- Z = ( 0g ` R ) $.
    invrl.t $e |- T = ( .r ` R ) $.
    invrl.u $e |- U = ( 1r ` R ) $.
    invrl.i $e |- I = ( invr ` R ) $.
    $( Property of the multiplicative inverse in a division ring.  ( ~ recid2
       analog.) $)
    drnginvrl $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( ( I ` X ) T X ) = U ) $=
      ( cdr wcel wne cfv co wceq wa cui drngunit crg wi drngrng unitlinv ex syl
      eqid sylbird 3impib ) BMNZFANZFGOZFEPFCQDRZUKULUMSFBTPZNZUNABUOFGHUOUHZIU
      AUKBUBNZUPUNUCBUDURUPUNBCUOEDFUQLJKUEUFUGUIUJ $.
      $( [19-Apr-2014] $)

    $( Property of the multiplicative inverse in a division ring.  ( ~ recid
       analog.) $)
    drnginvrr $p |- ( ( R e. DivRing /\ X e. B /\ X =/= Z )
             -> ( X T ( I ` X ) ) = U ) $=
      ( cdr wcel wne cfv co wceq wa cui drngunit crg wi drngrng unitrinv ex syl
      eqid sylbird 3impib ) BMNZFANZFGOZFFEPCQDRZUKULUMSFBTPZNZUNABUOFGHUOUHZIU
      AUKBUBNZUPUNUCBUDURUPUNBCUOEDFUQLJKUEUFUGUIUJ $.
      $( [19-Apr-2014] $)
  $}

  ${
    drngmuleq0.b $e |- B = ( Base ` R ) $.
    drngmuleq0.o $e |- O = ( 0g ` R ) $.
    drngmuleq0.t $e |- T = ( .r ` R ) $.
    drngmuleq0.r $e |- ( ph -> R e. DivRing ) $.
    drngmuleq0.x $e |- ( ph -> X e. B ) $.
    drngmuleq0.y $e |- ( ph -> Y e. B ) $.
    $( A product is zero iff one of its factors is zero. $)
    drngmul0or $p |- ( ph -> ( ( X T Y ) = O <-> ( X = O \/ Y = O ) ) ) $=
      ( co wceq wa cfv wcel adantr syl2anc wo wn wne df-ne cinvr oveq2 ad2antlr
      cur cdr simpr eqid drnginvrl syl3anc oveq1d crg drngrng drnginvrcl rngass
      syl13anc rnglidm 3eqtr3d adantlr rngrz ex syl5bir orrd rnglz oveq1 eqeq1d
      syl syl5ibrcom jaod impbid ) AFGDNZEOZFEOZGEOZUAZAVOVRAVOPZVPVQVPUBFEUCZV
      SVQFEUDVSVTVQVSVTPZFCUEQZQZVNDNZWCEDNZGEVOWDWEOAVTVNEWCDUFUGAVTWDGOVOAVTP
      ZWCFDNZGDNZCUHQZGDNZWDGWFWGWIGDWFCUIRZFBRZVTWGWIOAWKVTKSZAWLVTLSZAVTUJZBC
      DWIWBFEHIJWIUKZWBUKZULUMUNWFCUORZWCBRZWLGBRZWHWDOAWRVTAWKWRKCUPVJZSWFWKWL
      VTWSWMWNWOBCWBFEHIWQUQUMZWNAWTVTMSBCDWCFGHJURUSAWJGOZVTAWRWTXCXAMBCDWIGHJ
      WPUTTSVAVBWAWRWSWEEOVSWRVTAWRVOXASSAVTWSVOXBVBBCDWCEHJIVCTVAVDVEVFVDAVPVO
      VQAVOVPEGDNZEOZAWRWTXEXAMBCDGEHJIVGTVPVNXDEFEGDVHVIVKAVOVQFEDNZEOZAWRWLXG
      XALBCDFEHJIVCTVQVNXFEGEFDUFVIVKVLVM $.
      $( [8-Oct-2014] $)

    $( A product is nonzero iff both its factors are nonzero. $)
    drngmulne0 $p |- ( ph
     -> ( ( X T Y ) =/= O <-> ( X =/= O /\ Y =/= O ) ) ) $=
      ( co wne wceq wo wn wa drngmul0or necon3abid neanior syl6bbr ) AFGDNZEOFE
      PGEPQZRFEOGEOSAUEUDEABCDEFGHIJKLMTUAFEGEUBUC $.
      $( [18-Oct-2014] $)

    drngmuleq0.e $e |- ( ph -> Y =/= O ) $.
    $( An element is zero iff its product with a nonzero element is zero. $)
    drngmuleq0 $p |- ( ph -> ( ( X T Y ) = O <-> X = O ) ) $=
      ( co wceq wo drngmul0or wne wb wn df-ne orel2 orc impbid1 sylbi syl bitrd
      ) AFGDOEPFEPZGEPZQZUIABCDEFGHIJKLMRAGESZUKUITZNULUJUAZUMGEUBUNUKUIUJUIUCU
      IUJUDUEUFUGUH $.
      $( [8-Oct-2014] $)
  $}

