$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Abel $.

  $( Extend class notation with class of all Abelian groups. $)
  cabel $a class Abel $.

  ${
    $d a b g $.
    $( Define class of all Abelian groups. $)
    df-abl $a |- Abel = { g e. Grp | A. a e. ( Base ` g )
      A. b e. ( Base ` g ) ( a ( +g ` g ) b ) = ( b ( +g ` g ) a ) } $.
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g P $.
    isabl.b $e |- B = ( Base ` G ) $.
    isabl.p $e |- P = ( +g ` G ) $.
    $( The predicate "is an Abelian (commutative) group." $)
    isabl $p |- ( G e. Abel <-> ( G e. Grp /\
             A. x e. B A. y e. B ( x P y ) = ( y P x ) ) ) $=
      ( vg cv cplusg cfv co wceq cbs wral cgrp cabel fveq2 syl6eqr oveqd wb syl
      raleq raleqbi1dv eqeq12d 2ralbidv bitrd df-abl elrab2 ) AIZBIZHIZJKZLZUKU
      JUMLZMZBULNKZOZAUQOZUJUKDLZUKUJDLZMZBCOACOZHEPQULEMZUSUPBCOZACOZVCVDUQCMU
      SVFUAVDUQENKCULENRFSURVEAUQCUPBUQCUCUDUBVDUPVBABCCVDUNUTUOVAVDUMDUJUKVDUM
      EJKDULEJRGSZTVDUMDUKUJVGTUEUFUGHABUHUI $.
      $( [17-Oct-2011] $)
  $}

  ${
    $d a b K $.  $d a b L $.
    ablprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    ablprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    $( If two structures have the same group components (properties), one is an
       Abelian group iff the other one is. $)
    ablprop $p |- ( K e. Abel <-> L e. Abel ) $=
      ( va vb cgrp wcel cv cplusg cfv co wceq cbs wral cabel grpprop eqid isabl
      wa anbi1i 3bitr4i ) AGHZEIZFIZAJKZLUEUDUFLMFANKZOEUGOZTBGHZUHTAPHBPHUCUIU
      HABCDQUAEFUGUFAUGRUFRSEFUGUFBCDSUB $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d x y ph $.
    isabld.g $e |- ( ph -> G e. Grp ) $.
    isabld.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isabld.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isabld.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) = ( y P x ) ) $.
    $( Properties that determine an Abelian group. $)
    isabld $p |- ( ph -> G e. Abel ) $=
      ( cgrp wcel cv cfv co wceq wral oveqd raleqbidv eqid cplusg cabel eqeq12d
      cbs wa anbi2d 3expib ralrimivv mpbi2and isabl sylibr ) AFKLZBMZCMZFUANZOZ
      UNUMUOOZPZCFUDNZQZBUSQZUEZFUBLAULUMUNEOZUNUMEOZPZCDQZBDQZVBAVGVAULAVFUTBD
      USHAVEURCDUSHAVCUPVDUQAEUOUMUNIRAEUOUNUMIRUCSSUFGAVEBCDDAUMDLUNDLVEJUGUHU
      IBCUSUOFUSTUOTUJUK $.
      $( [6-Aug-2013] $)
  $}

  ${
    $d x y B $.  $d x y G $.
    isabli.g $e |- G e. Grp $.
    isabli.b $e |- B = ( Base ` G ) $.
    isabli.p $e |- P = ( +g ` G ) $.
    isabli.c $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) = ( y P x ) ) $.
    $( Properties that determine an Abelian group. $)
    isabli $p |- G e. Abel $=
      ( cabel wcel cgrp cv co wceq wral isabl rgen2a mpbir2an ) EJKELKAMZBMZDNU
      ATDNOZBCPACPABCDEGHQFUBABCIRS $.
      $( [4-Sep-2011] $)
  $}

  ${
    $d x y G $.
    $( An Abelian group is a group. $)
    ablgrp $p |- ( G e. Abel -> G e. Grp ) $=
      ( vx vy cabel wcel cgrp cv cplusg cfv co wceq cbs wral eqid isabl simplbi
      ) ADEAFEBGZCGZAHIZJRQSJKCALIZMBTMBCTSATNSNOP $.
      $( [26-Aug-2011] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d x y P $.  $d x y X $.  $d y Y $.
    ablcom.b $e |- B = ( Base ` G ) $.
    ablcom.p $e |- P = ( +g ` G ) $.
    $( An Abelian group operation is commutative. $)
    ablcom $p |- ( ( G e. Abel /\ X e. B /\ Y e. B ) ->
                 ( X P Y ) = ( Y P X ) ) $=
      ( vx vy cabel wcel co wceq cv wral wa cgrp oveq1 oveq2 eqeq12d simprbi
      isabl rcla42v syl5com 3impib ) CJKZDAKZEAKZDEBLZEDBLZMZUFHNZINZBLZUMULBLZ
      MZIAOHAOZUGUHPUKUFCQKUQHIABCFGUBUAUPUKDUMBLZUMDBLZMHIDEAAULDMUNURUOUSULDU
      MBRULDUMBSTUMEMURUIUSUJUMEDBSUMEDBRTUCUDUE $.
      $( [26-Aug-2011] $)

    $( Commutative/associative law for Abelian groups. $)
    abl32 $p |- ( ( G e. Abel /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                 ( ( X P Y ) P Z ) = ( ( X P Z ) P Y ) ) $=
      ( cabel wcel w3a wa co wceq ablcom 3adant3r1 oveq2d cgrp ablgrp grpass
      sylan adantr simpr1 simpr3 simpr2 syl13anc 3eqtr4d ) CIJZDAJZEAJZFAJZKZLZ
      DEFBMZBMZDFEBMZBMZDEBMFBMZDFBMEBMZUMUNUPDBUHUJUKUNUPNUIABCEFGHOPQUHCRJZUL
      URUONCSZABCDEFGHTUAUMUTUIUKUJUSUQNUHUTULVAUBUHUIUJUKUCUHUIUJUKUDUHUIUJUKU
      EABCDFEGHTUFUG $.
      $( [4-Feb-2014] $)

    $( Commutative/associative law for Abelian groups. $)
    abl4 $p |- ( ( G e. Abel /\ ( X e. B /\ Y e. B ) /\ ( Z e. B /\ W e. B ) )
       -> ( ( X P Y ) P ( Z P W ) ) = ( ( X P Z ) P ( Y P W ) ) ) $=
      ( cabel wcel wa w3a co wceq simp1 syl13anc grpcl syl3anc grpass 3ad2ant1
      simp2l simp2r simp3l abl32 oveq1d cgrp ablgrp simp3r 3eqtr3d ) CJKZEAKZFA
      KZLZGAKZDAKZLZMZEFBNZGBNZDBNZEGBNZFBNZDBNZUSGDBNBNZVBFDBNBNZURUTVCDBURUKU
      LUMUOUTVCOUKUNUQPUKULUMUQUBZUKULUMUQUCZUKUNUOUPUDZABCEFGHIUEQUFURCUGKZUSA
      KZUOUPVAVEOUKUNVJUQCUHUAZURVJULUMVKVLVGVHABCEFHIRSVIUKUNUOUPUIZABCUSGDHIT
      QURVJVBAKZUMUPVDVFOVLURVJULUOVNVLVGVIABCEGHIRSVHVMABCVBFDHITQUJ $.
      $( [4-Feb-2014] $)
  $}

  ${
    ablinvadd.b $e |- B = ( Base ` G ) $.
    ablinvadd.p $e |- P = ( +g ` G ) $.
    ablinvadd.n $e |- N = ( invg ` G ) $.
    $( The inverse of an Abelian group operation. $)
    ablinvadd $p |- ( ( G e. Abel /\ X e. B /\ Y e. B ) ->
                   ( N ` ( X P Y ) ) = ( ( N ` X ) P ( N ` Y ) ) ) $=
      ( cabel wcel w3a co cfv cgrp wceq ablgrp grpinvadd grpinvcl syl2anc simp1
      syl3an1 3ad2ant1 simp2 simp3 ablcom syl3anc eqtr4d ) CJKZEAKZFAKZLZEFBMDN
      ZFDNZEDNZBMZUOUNBMZUICOKZUJUKUMUPPCQZABCDEFGHIRUBULUIUOAKZUNAKZUQUPPUIUJU
      KUAULURUJUTUIUJURUKUSUCZUIUJUKUDACDEGISTULURUKVAVBUIUJUKUEACDFGISTABCUOUN
      GHUFUGUH $.
      $( [31-Mar-2014] $)
  $}

  ${
    ablsubadd.b $e |- B = ( Base ` G ) $.
    ablsubadd.p $e |- P = ( +g ` G ) $.
    ablsubadd.m $e |- M = ( -g ` G ) $.
    $( Relationship between Abelian group subtraction and addition. $)
    ablsubadd $p |- ( ( G e. Abel /\ ( X e. B /\ Y e. B /\ Z e. B ) )
          -> ( ( X M Y ) = Z <-> ( Y P Z ) = X ) ) $=
      ( cabel wcel w3a wa co wceq cgrp wb ablgrp grpsubadd ablcom eqeq1d bitr4d
      sylan 3adant3r1 ) CKLZEALZFALZGALZMZNZEFDOGPZGFBOZEPZFGBOZEPUFCQLUJULUNRC
      SABCDEFGHIJTUDUKUOUMEUFUHUIUOUMPUGABCFGHIUAUEUBUC $.
      $( [31-Mar-2014] $)
  $}

  ${
    ablsub4.b $e |- B = ( Base ` G ) $.
    ablsub4.p $e |- P = ( +g ` G ) $.
    ablsub4.m $e |- M = ( -g ` G ) $.
    $( Commutative/associative subtraction law for Abelian groups. $)
    ablsub4 $p |- ( ( G e. Abel /\ ( X e. B /\ Y e. B ) /\ ( Z e. B /\ W e. B )
        )
       -> ( ( X P Y ) M ( Z P W ) ) = ( ( X M Z ) P ( Y M W ) ) ) $=
      ( cabel wcel wa co cfv wceq grpcl syl3anc grpsubval w3a simp1 cgrp ablgrp
      cminusg 3ad2ant1 simp2l simp2r simp3l simp3r eqid simp2 syl2anc syl112anc
      grpinvcl abl4 ablinvadd oveq2d oveq12d 3eqtr4d eqtrd ) CLMZFAMZGAMZNZHAMZ
      EAMZNZUAZFGBOZHEBOZDOZVJVKCUEPZPZBOZFHDOZGEDOZBOZVIVBVJAMZVKAMZVLVOQVBVEV
      HUBZVICUCMZVCVDVSVBVEWBVHCUDUFZVBVCVDVHUGZVBVCVDVHUHZABCFGIJRSVIWBVFVGVTW
      CVBVEVFVGUIZVBVEVFVGUJZABCHEIJRSABCVMDLVJVKIJVMUKZKTSVIVJHVMPZEVMPZBOZBOZ
      FWIBOZGWJBOZBOZVOVRVIVBVEWIAMZWJAMZWLWOQWAVBVEVHULVIWBVFWPWCWFACVMHIWHUOU
      MVIWBVGWQWCWGACVMEIWHUOUMABCWJFGWIIJUPUNVIVNWKVJBVIVBVFVGVNWKQWAWFWGABCVM
      HEIJWHUQSURVIVPWMVQWNBVIVBVCVFVPWMQWAWDWFABCVMDLFHIJWHKTSVIVBVDVGVQWNQWAW
      EWGABCVMDLGEIJWHKTSUSUTVA $.
      $( [31-Mar-2014] $)

    $( Abelian group addition/subtraction law. $)
    abladdsub4 $p |- ( ( G e. Abel
       /\ ( X e. B /\ Y e. B ) /\ ( Z e. B /\ W e. B ) )
   -> ( ( X P Y ) = ( Z P W ) <-> ( X M Z ) = ( W M Y ) ) ) $=
      ( wcel wa co wceq grpcl syl3anc ablsub4 syl122anc syl2anc w3a cgrp ablgrp
      cabel wb 3ad2ant1 simp2l simp2r simp3l simp3r grpsubrcan syl13anc c0g cfv
      simp1 grpsubid oveq2d grpsubcl grprid 3eqtrd oveq1d grplid eqeq12d bitr3d
      eqid ) CUDLZFALZGALZMZHALZEALZMZUAZFGBNZHGBNZDNZHEBNZVODNZOZVNVQOZFHDNZEG
      DNZOVMCUBLZVNALZVQALZVOALZVSVTUEVFVIWCVLCUCUFZVMWCVGVHWDWGVFVGVHVLUGZVFVG
      VHVLUHZABCFGIJPQVMWCVJVKWEWGVFVIVJVKUIZVFVIVJVKUJZABCHEIJPQVMWCVJVHWFWGWJ
      WIABCHGIJPQACDVNVQVOIKUKULVMVPWAVRWBVMVPWAGGDNZBNZWACUMUNZBNZWAVMVFVGVHVJ
      VHVPWMOVFVIVLUOZWHWIWJWIABCDGFGHIJKRSVMWLWNWABVMWCVHWLWNOWGWIACDWNGIWNVEZ
      KUPTUQVMWCWAALZWOWAOWGVMWCVGVJWRWGWHWJACDFHIKURQABCWNWAIJWQUSTUTVMVRHHDNZ
      WBBNZWNWBBNZWBVMVFVJVKVJVHVRWTOWPWJWKWJWIABCDGHEHIJKRSVMWSWNWBBVMWCVJWSWN
      OWGWJACDWNHIWQKUPTVAVMWCWBALZXAWBOWGVMWCVKVHXBWGWKWIACDEGIKURQABCWNWBIJWQ
      VBTUTVCVD $.
      $( [31-Mar-2014] $)

    $( Associative-type law for group subtraction and addition. $)
    abladdsub $p |- ( ( G e. Abel /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
         ( ( X P Y ) M Z ) = ( ( X M Z ) P Y ) ) $=
      ( cabel wcel w3a wa co wceq ablcom 3adant3r3 oveq1d syl3anc ablgrp adantr
      cgrp simpr2 simpr1 simpr3 grpaddsubass syl13anc simpl grpsubcl 3eqtrd ) C
      KLZEALZFALZGALZMZNZEFBOZGDOFEBOZGDOZFEGDOZBOZVAFBOZUQURUSGDULUMUNURUSPUOA
      BCEFHIQRSUQCUCLZUNUMUOUTVBPULVDUPCUAUBZULUMUNUOUDZULUMUNUOUEZULUMUNUOUFZA
      BCDFEGHIJUGUHUQULUNVAALZVBVCPULUPUIVFUQVDUMUOVIVEVGVHACDEGHJUJTABCFVAHIQT
      UK $.
      $( [19-Apr-2014] $)

    $( Cancellation law for subtraction. $)
    ablpncan2 $p |- ( ( G e. Abel /\ X e. B /\ Y e. B )
       -> ( ( X P Y ) M X ) = Y ) $=
      ( cabel wcel w3a co c0g cfv wceq simp1 simp2 simp3 syl2anc abladdsub cgrp
      syl13anc ablgrp syl eqid grpsubid oveq1d grplid 3eqtrd ) CJKZEAKZFAKZLZEF
      BMEDMZEEDMZFBMZCNOZFBMZFUNUKULUMULUOUQPUKULUMQZUKULUMRZUKULUMSZVAABCDEFEG
      HIUAUCUNUPURFBUNCUBKZULUPURPUNUKVCUTCUDUEZVAACDUREGURUFZIUGTUHUNVCUMUSFPV
      DVBABCURFGHVEUITUJ $.
      $( [2-Oct-2014] $)
  $}

  ${
    $d x y z G $.
    cnaddablx.g $e |- G = { <. 1 , CC >. , <. 2 , + >. } $.
    $( The complex numbers are an Abelian group under addition.  This version
       of ~ cnaddabl shows the explicit structure "scaffold" we chose for the
       definition for Abelian groups.  Note:  This theorem has hard-coded
       structure indices for demonstration purposes.  It is not intended for
       general use; use ~ cnaddabl instead. $)
    cnaddablx $p |- G e. Abel $=
      ( vx vy vz cc caddc cv cneg cc0 cnex addex ax-addcl addass 0cn negcl wcel
      addid2 co addcom mpdan negid eqtr3d isgrpix grpbasex grpplusgx isabli
      wceq ) CDFGACDEFGACHZIZJKLBUIDHZMUIUKEHNOUIRUIPZUIFQZUIUJGSZUJUIGSZJUMUJF
      QUNUOUHULUIUJTUAUIUBUCUDFGAKLBUEFGAKLBUFUIUKTUG $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d x y z G $.
    cnaddabl.g $e |- G
               = { <. ( Base ` ndx ) , CC >. , <. ( +g ` ndx ) , + >. } $.
    $( The complex numbers are an Abelian group under addition.  This version
       of ~ cnaddablx hides the explicit structure indices i.e. is
       "scaffold-independent".  Note that the proof also does not reference
       explicit structure indices.  The actual structure is dependent on how
       ` Base ` and ` +g ` is defined. $)
    cnaddabl $p |- G e. Abel $=
      ( vx vy vz cc caddc cc0 cnx cbs cfv cop cplusg cvv wcel wceq ax-mp addcom
      cv co cneg cpr prex eqeltri grpbase addex grpplusg ax-addcl addass addid2
      cnex 0cn negcl mpdan negid eqtr3d isgrpi isabli ) CDFGACDEFGACSZUAZHAIJKF
      LZIMKGLZUBNBVAVBUCUDFNOFAJKPUKFGANBUEQZGNOGAMKPUFFGANBUGQZUSDSZUHUSVEESUI
      ULUSUJUSUMZUSFOZUSUTGTZUTUSGTZHVGUTFOVHVIPVFUSUTRUNUSUOUPUQVCVDUSVERUR $.
      $( [20-Oct-2012] $)
  $}

  ${
    $d x y z G $.
    zaddablx.g $e |- G = { <. 1 , ZZ >. , <. 2 , + >. } $.
    $( The integers are an Abelian group under addition.  Note:  This theorem
       has hard-coded structure indices for demonstration purposes.  It is not
       intended for general use. $)
    zaddablx $p |- G e. Abel $=
      ( vx vy vz cz caddc cv cneg cc0 zex addex wcel wceq zcn syl addcom syl2an
      cc co zaddcl addass syl3an 0z addid2 znegcl mpdan eqtr3d isgrpix grpbasex
      negid grpplusgx isabli ) CDFGACDEFGACHZIZJKLBUNDHZUAUNFMZUNSMZUPFMZUPSMZE
      HZFMVASMUNUPGTZVAGTUNUPVAGTGTNUNOZUPOZVAOUNUPVAUBUCUDUQURJUNGTUNNVCUNUEPU
      NUFZUQUNUOGTZUOUNGTZJUQUOFMZVFVGNZVEUQURUOSMVIVHVCUOOUNUOQRUGUQURVFJNVCUN
      UKPUHUIFGAKLBUJFGAKLBULUQURUTVBUPUNGTNUSVCVDUNUPQRUM $.
      $( [4-Sep-2011] $)
  $}

