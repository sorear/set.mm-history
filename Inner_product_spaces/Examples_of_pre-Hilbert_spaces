$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Examples of pre-Hilbert spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The set of complex numbers is an inner product (pre-Hilbert) space.
       (Contributed by Steve Rodriguez, 28-Apr-2007.) $)
    cnphl $p |- <. <. + , x. >. , abs >. e. CPreHil $=
      ( vx vy caddc cmul cop cabs cphl wcel cncv cv co cfv c2 cexp c1 cneg
      wceq cc wral cvv wa wb addex mulex ccj csqr copab df-abs axcnex
      funopabex2 eqeltr cabl cgr cnaddabl ablgrp ax-mp axaddopr grprn isphlg
      mp3an cnncv cmin mulm1t adantl opreq2d negsubt eqtrd fveq2d opreq1d cre
      sqabsaddt sqabssubt opreq12d ppncant axaddcl absclt recnd sqclt syl
      syl2an axmulcl cjclt sylan2 reclt 2cn mpan 3syl syl3anc 2timest eqcomd
      3eqtrd rgen2 mpbir2an ) CDEFEZGHZWNIHZAJZBJZCKFLMNKZWQOPWRDKZCKZFLZMNKZCK
      ZMWQFLZMNKZWRFLZMNKZCKZDKZQZBRSARSZCTHDTHFTHWOWPXLUAUBUCUDFWQRHZWRWQWQUEL
      DKUFLZQUAABUGTABUHABRXNUIUJUKABTTTDCFRCRCULHCUMHUNCUOUPUQURUSUTVAXKABRXMW
      RRHZUAZXDWSWQWRVBKZFLZMNKZCKZXIXICKZXJXPXCXSWSCXPXBXRMNXPXAXQFXPXAWQWRPZC
      KXQXPWTYBWQCXOWTYBQXMWRVCVDVEWQWRVFVGVHVIVEXPXTXIMWQWRUELZDKZVJLZDKZCKZXI
      YFVBKZCKZYAXPWSYGXSYHCWQWRVKWQWRVLVMXIRHZYFRHZYJYIYAQXPXIYFXIVNXFRHZXHRHZ
      YJXMXOXFXHVOXMXERHYLXMXEWQVPVQXEVRVSXOXGRHYMXOXGWRVPVQXGVRVSVTZXPYDRHZYER
      HZYKXMYCRHYOXOWQYCWAWRWBWCYOYEYDWDVQMRHYPYKWEMYEWAWFWGYNWHVGXPYJYAXJQYNYJ
      XJYAXIWIWJVSWKWLWM $.
      $( [29-Apr-2007] $) $( [28-Apr-2007] $)
  $}

  ${
    elimphl.1 $e |- W = ( 1st ` U ) $.
    elimphl.2 $e |- G = ( 1st ` W ) $.
    elimphl.5 $e |- X = ran G $.
    elimphl.6 $e |- U e. CPreHil $.
    $( Hypothesis elimination lemma for complex inner product spaces to assist
       weak deduction theorem. $)
    elimphl $p |- if ( A e. X , A , ( Id ` G ) ) e. X $=
      ( cgi cfv cgr wcel cvc cncv cphl phlncv ax-mp ncvv vcgrp eqid grpidcl
      elimel ) ACJKZECLMZUDEMDNMZUEBOMZUFBPMUGIBQRBDFSRCDGTRUDCEHUDUAUBRUC $.
      $( [?] $) $( [27-Apr-2007] $)
  $}

  ${
    elimphl4.1 $e |- W = ( 1st ` U ) $.
    elimphl4.2 $e |- G = ( 1st ` W ) $.
    elimphl4.5 $e |- X = ran G $.
    elimphl4.6 $e |- ( ph <->
                     ( U e. CPreHil /\ ( A e. X /\ B e. X /\ C e. X ) ) ) $.
    elimphl4.7 $e |- T = if ( ph , U , <. <. + , x. >. , abs >. ) $.
    $( Hypothesis elimination lemma for normed complex vector spaces
       to assist weak deduction theorem. $)
    elimphl4 $p |- ( T e. CPreHil /\
                  ( if ( ph , A , 0 ) e. ran ( 1st ` ( 1st ` T ) )
                 /\ if ( ph , B , 0 ) e. ran ( 1st ` ( 1st ` T ) )
                 /\ if ( ph , C , 0 ) e. ran ( 1st ` ( 1st ` T ) ) ) ) $=
      ( cphl wcel cc0 cif c1st cfv crn w3a wa caddc cmul cop cabs wceq wb
      eqeq2i eleq1 fveq2 fveq2d rneqd fveq2i eqtr rneqi syl5eq eleq2d
      3anbi123d anbi12d syl5bb sylbir 3anbi1d anbi2d 3anbi2d 3anbi3d cnphl
      eqid cnncv0 3pm3.2i pm3.2i elimhyp4v ) AEOPZABQRZESTZSTZUAZPZACQRZVRPZADQ
      RZVRPZUBZUCVNBVRPZCVRPZDVRPZUBZUCZVNVSWFWGUBZUCVNVSWAWGUBZUCFBCUDUEUFUGUF
      ZQQWLOPZQWLSTZSTZUAZPZWQWQUBZUCZVNQVRPZWTWTUBZUCZVNVSWTWTUBZUCVNVSWAWTUBZ
      UCDQFAFWLRZUHFEUHZAWIUIEXEFNUJXFFOPZBIPZCIPZDIPZUBZUCWIAXFXGVNXKWHFEOUKXF
      XHWEXIWFXJWGXFIVRBXFFSTZSTZUAZVRIXFXMVQXFXLVPSFESULUMUNIGUAXNLGXMGHSTXMKH
      XLSJUOUPUQUPURZUSXFIVRCXOUSXFIVRDXOUSUTVAMVBVCBVOUHZWHWJVNXPWEVSWFWGBVOVR
      UKVDVECVTUHZWJWKVNXQWFWAVSWGCVTVRUKVFVEDWBUHZWKWDVNXRWGWCVSWADWBVRUKVGVEW
      LXEUHWLEUHZWSXBUIEXEWLNUJXSWMVNWRXAWLEOUKXSWQWTWQWTWQWTXSWPVRQXSWOVQXSWNV
      PSWLESULUMUNUSZXTXTUTVAVCQVOUHZXAXCVNYAWTVSWTWTQVOVRUKVDVEQVTUHZXCXDVNYBW
      TWAVSWTQVTVRUKVFVEQWBUHZXDWDVNYCWTWCVSWAQWBVRUKVGVEWMWRVHWQWQWQWLWOWNWPWN
      VIWOVIWPVIWLVIVJZYDYDVKVLVM $.
      $( [?] $) $( [22-Apr-2007] $)
  $}

