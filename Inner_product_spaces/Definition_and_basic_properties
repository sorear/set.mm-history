$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CPreHil $.

  $( Extend class notation with the class of all complex Hilbert spaces. $)
  cphl $a class CPreHil $.

  ${
    $d g n s w x y $.
    $( Define the class of all complex inner product spaces.  An inner product
       space is a normed vector space whose norm satisfies the parallelogram
       law (a property that induces an inner product).  Based on Exercise 4(b)
       of [ReedSimon] p. 63.  The vector operation is ` g ` , the scalar
       product is ` s ` , and the norm is ` n ` .  An inner product space is
       also called a pre-Hilbert space. $)
    df-phl $a |- CPreHil = ( NrmCVec i^i { <. <. g , s >. , n >. |
          A. x e. ran g A. y e. ran g
       ( ( ( n ` ( x g y ) ) ^ 2 ) + ( ( n ` ( x g ( -u 1 s y ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( n ` x ) ^ 2 ) + ( ( n ` y ) ^ 2 ) ) ) } ) $.

    $( Every complex inner product space is a normed complex vector space. $)
    phlncv $p |- ( U e. CPreHil -> U e. NrmCVec ) $=
      ( vx vy vg vn vs cphl cncv cv co cfv c2 cexp c1 cneg caddc cmul wceq crn
      wral copab2 cin df-phl inss1 eqsstr sseli ) GHAGHBIZCIZDIZJEIZKLMJUGNOUHF
      IJUIJUJKLMJPJLUGUJKLMJUHUJKLMJPJQJRCUISZTBUKTDFEUAZUBHBCDEFUCHULUDUEUF $.
      $( [12-Apr-2007] $) $( [2-Apr-2007] $)

    $( The class of all complex inner product spaces is a relation. $)
    phlrel $p |- Rel CPreHil $=
      ( vx cphl cncv wss wrel cv phlncv ssriv ncvrel relss mp2 ) BCDCEBEABCAFGH
      IBCJK $.
      $( [18-Apr-2007] $) $( [2-Apr-2007] $)
  $}

  ${
    $d g n s x y G $.  $d g n s x y N $.  $d g n s x y S $.  $d g n s x y X $.
    $d g n s Z $.
    isphlg.1 $e |- X = ran G $.
    $( The predicate "is a complex inner product space."  An inner product
       space is a normed vector space whose norm satisfies the parallelogram
       law.  The vector (group) addition operation is ` G ` , the scalar
       product is ` S ` , and the norm is ` N ` .  An inner product space is
       also called a pre-Hilbert space. $)
    isphlg $p |- ( ( G e. A /\ S e. B /\ N e. C ) ->
  ( <. <. G , S >. , N >. e. CPreHil <-> ( <. <. G , S >. , N >. e. NrmCVec /\
          A. x e. X A. y e. X
      ( ( ( N ` ( x G y ) ) ^ 2 ) + ( ( N ` ( x G ( -u 1 S y ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( N ` x ) ^ 2 ) + ( ( N ` y ) ^ 2 ) ) ) ) ) ) $=
      ( vg vn vs wcel w3a cop cncv cv co cfv c2 cexp c1 cneg caddc cmul wceq
      crn wral copab2 wa cphl rneq syl6eqr raleq1d opreq fveq2d opreq1d
      opreq12d eqeq1d ralbidv bitrd opreq2d 2ralbidv fveq1 eqeq12d eloprabg
      anbi2d cin df-phl eleq2i elin bitr syl5bb ) GCNFDNHENOZGFPHPZQNZVPARZBRZK
      RZSZLRZTZUAUBSZVRUCUDZVSMRZSZVTSZWBTZUAUBSZUESZUAVRWBTZUAUBSZVSWBTZUAUBSZ
      UESZUFSZUGZBVTUHZUIZAWSUIZKMLUJZNZUKZVQVRVSGSZHTZUAUBSZVRWEVSFSZGSZHTZUAU
      BSZUESZUAVRHTZUAUBSZVSHTZUAUBSZUESZUFSZUGZBIUIZAIUIZUKVPULNZVOXCYAVQXAXEW
      BTZUAUBSZVRWGGSZWBTZUAUBSZUESZWQUGZBIUIZAIUIZYDXIWBTZUAUBSZUESZWQUGZBIUIZ
      AIUIYAKMLGFHCDEVTGUGZXAWTAIUIYKYQWTAWSIYQWSGUHIVTGUMJUNZUOYQWTYJAIYQWTWRB
      IUIYJYQWRBWSIYRUOYQWRYIBIYQWKYHWQYQWDYDWJYGUEYQWCYCUAUBYQWAXEWBVRVSVTGUPU
      QURYQWIYFUAUBYQWHYEWBVRWGVTGUPUQURUSUTVAVBVAVBWFFUGZYIYOABIIYSYHYNWQYSYGY
      MYDUEYSYFYLUAUBYSYEXIWBYSWGXHVRGWEVSWFFUPVCUQURVCUTVDWBHUGZYPXTAIYTYOXSBI
      YTYNXLWQXRYTYDXGYMXKUEYTYCXFUAUBXEWBHVEURYTYLXJUAUBXIWBHVEURUSYTWPXQUAUFY
      TWMXNWOXPUEYTWLXMUAUBVRWBHVEURYTWNXOUAUBVSWBHVEURUSVCVFVAVAVGVHYBVPQXBVIZ
      NXDULUUAVPABKLMVJVKVPQXBVLVMVN $.
      $( [20-Apr-2007] $) $( [2-Apr-2007] $)
  $}


  ${
    phlop.1 $e |- W = ( 1st ` U ) $.
    phlop.2 $e |- G = ( 1st ` W ) $.
    phlop.3 $e |- S = ( 2nd ` W ) $.
    phlop.4 $e |- N = ( 2nd ` U ) $.
    $( A complex inner product space in terms of ordered pair components. $)
    phlop $p |- ( U e. CPreHil -> U = <. <. G , S >. , N >. ) $=
      ( cphl wcel c1st cfv c2nd cop wrel wceq phlrel 1st2nd mpan cncv cvc
      phlncv ncvv vcrel opeq12i syl6eqr 3syl opeq1d syl5eqr eqtrd ) BJKZBBLMZBN
      MZOZCAOZDOZJPULBUOQRBJSTULEDOUQUOULEUPDULBUAKEUBKZEUPQBUCBEFUDUREELMZENMZ
      OZUPUBPUREVAQUEEUBSTCUSAUTGHUFUGUHUIEUMDUNFIUFUJUK $.
      $( [21-Apr-2007] $) $( [2-Apr-2007] $)
  $}

