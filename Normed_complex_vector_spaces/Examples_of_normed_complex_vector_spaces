$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Examples of normed complex vector spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    cnnv.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The set of complex numbers is a normed complex vector space.  The vector
       operation is ` + ` , the scalar product is ` x. ` , and the norm
       function is ` abs ` .  (Contributed by Steve Rodriguez, 3-Dec-2006.) $)
    cnnv $p |- U e. NrmCVec $=
      ( vx vy cmul caddc cabs cc cc0 cabl wcel cgr cnaddablo ablogrpo ax-mp
      cxp ax-addopr fdmi grprn cnid cnvc absf cv cfv wceq abs00 biimpa absmul
      abstri isnvi ) CDEAFGHIFHFJKFLKMFNOHHPHFQRSTUAUBCUCZHKUKGUDIUEUKIUEUKUFUG
      DUCZUKUHUKULUIBUJ $.
      $( [12-Jan-2008] $)
  $}

  ${
    cnnvg.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The vector addition (group) operation of the normed complex vector space
       of complex numbers. $)
    cnnvg $p |- + = ( +v ` U ) $=
      ( cpv cfv c1st caddc cmul cop eqid vafval cabs fveq2i opex op1st eqtri
      addex 3eqtrri ) ACDZAEDZEDFGHZEDFARRIJSTESTKHZEDTAUAEBLTKFGMNOLFGPNQ $.
      $( [12-Jan-2008] $)
  $}

  ${
    cnnvba.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The base set of the normed complex vector space of complex numbers. $)
    cnnvba $p |- CC = ( BaseSet ` U ) $=
      ( caddc crn cpv cfv cc cba cnnvg rneqi cabl wcel cgr cnaddablo ablogrpo
      ax-mp cxp ax-addopr fdmi grprn eqid bafval 3eqtr4i ) CDAEFZDGAHFZCUDABIJC
      GCKLCMLNCOPGGQGCRSTAUDUEUEUAUDUAUBUC $.
      $( [24-Jul-2008] $) $( [7-Nov-2007] $)
  $}

  $( Derive the associative law for complex number addition ~ ax-addass to
     demonstrate the use of ~ cnnv , ~ cnnvg , and ~ cnnvba . $)
  cnnvdemo $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) -> ( ( A + B ) + C ) =
      ( A + ( B + C ) ) ) $=
    ( caddc cmul cop cabs cnv wcel cc w3a co wceq eqid cnnv cnnvba cnnvg nvass
    mpan ) DEFGFZHIAJIBJICJIKABDLCDLABCDLDLMTTNZOABCTDJTUAPTUAQRS $.
    $( [12-Jan-2008] $)

  ${
    cnnvs.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The scalar product operation of the normed complex vector space of
       complex numbers. $)
    cnnvs $p |- x. = ( .s ` U ) $=
      ( cns cfv c1st c2nd caddc cmul cop eqid smfval cabs fveq2i opex op1st
      eqtri addex mulex op2nd 3eqtrri ) ACDZAEDZFDGHIZFDHUAAUAJKUBUCFUBUCLIZEDU
      CAUDEBMUCLGHNOPMGHQRST $.
      $( [12-Jan-2008] $)
  $}

  ${
    cnnvnm.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The norm operation of the normed complex vector space of complex
       numbers. $)
    cnnvnm $p |- abs = ( norm ` U ) $=
      ( cnm cfv c2nd caddc cmul cop cabs eqid nmfval fveq2i opex cc cr wf cvv
      wcel absf ax-cnex fex mp2an op2nd 3eqtrri ) ACDZAEDFGHZIHZEDIAUEUEJKAUGEB
      LUFIFGMNOIPNQRIQRSTNOQIUAUBUCUD $.
      $( [12-Jan-2008] $)
  $}

  ${
    $d x y z U $.
    cnnvm.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The vector subtraction operation of the normed complex vector space of
       complex numbers. $)
    cnnvm $p |- - = ( -v ` U ) $=
      ( vx vy vz cv cc wcel wa cmin co wceq copab2 c1 cneg cmul caddc cnsb cfv
      mulm1 adantl opreq2d negsub eqtr2d eqeq2d pm5.32i oprabbii cxp wfn wf
      subopr ffn ax-mp fnoprv mpbi cnv cnnv cnnvba cnnvg cnnvs eqid nvmfval
      3eqtr4i ) CFZGHZDFZGHZIZEFZVDVFJKZLZIZCDEMZVHVIVDNOVFPKZQKZLZIZCDEMZJARSZ
      VLVQCDEVHVKVPVHVJVOVIVHVOVDVFOZQKVJVHVNVTVDQVGVNVTLVEVFTUAUBVDVFUCUDUEUFU
      GJGGUHZUIZJVMLWAGJUJWBUKWAGJULUMCDEGGJUNUOAUPHVSVRLABUQCDEPAQVSGABURABUSA
      BUTVSVAVBUMVC $.
      $( [12-Jan-2008] $)
  $}

  ${
    elimnv.1 $e |- X = ( BaseSet ` U ) $.
    elimnv.5 $e |- Z = ( 0vec ` U ) $.
    elimnv.9 $e |- U e. NrmCVec $.
    $( Hypothesis elimination lemma for normed complex vector spaces to assist
       weak deduction theorem. $)
    elimnv $p |- if ( A e. X , A , Z ) e. X $=
      ( cnv wcel nvzcl ax-mp elimel ) ADCBHIDCIGBCDEFJKL $.
      $( [16-May-2007] $)
  $}

  $( Hypothesis elimination lemma for normed complex vector spaces to assist
     weak deduction theorem. $)
  elimnvu $p |- if ( U e. NrmCVec , U , <. <. + , x. >. , abs >. )
                 e. NrmCVec $=
    ( caddc cmul cop cabs cnv eqid cnnv elimel ) ABCDEDZFJJGHI $.
    $( [16-May-2007] $)

