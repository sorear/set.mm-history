  ${
    nvop.2 $e |- G = ( +v ` U ) $.
    nvop.4 $e |- S = ( .s ` U ) $.
    nvop.6 $e |- N = ( norm ` U ) $.
    $( A complex inner product space in terms of ordered pair components. $)
    nvop $p |- ( U e. NrmCVec -> U = <. <. G , S >. , N >. ) $=
      ( cnv wcel c1st cfv c2nd cop wrel wceq nvrel 1st2nd mpan eqid nvvop
      opeq1d nmfval opeq2i syl5eqr eqtrd ) BHIZBBJKZBLKZMZCAMZDMZHNUFBUIOPBHQRU
      FUGDMUKUIUFUGUJDABCUGUGSEFTUADUHUGBDGUBUCUDUE $.
      $( [25-Feb-2008] $) $( [11-Sep-2007] $)
  $}
$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Examples of normed complex vector spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    cnnv.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The set of complex numbers is a normed complex vector space.  The vector
       operation is ` + ` , the scalar product is ` x. ` , and the norm
       function is ` abs ` .  (Contributed by Steve Rodriguez, 3-Dec-2006.) $)
    cnnv $p |- U e. NrmCVec $=
      ( vx vy cmul caddc cabs cc cc0 addex mulex cabl wcel cgr cnaddabl ablgrp
      ax-mp axaddopr grprnOLD cnid cnvc absf cv cfv wceq abs00t biimpa absmult
      abstrit isnvi ) CDEAFGHIJKFHFLMFNMOFPQRSTUAUBCUCZHMUKGUDIUEUKIUEUKUFUGDUC
      ZUKUHUKULUIBUJ $.
      $( [12-Jan-2008] $) $( [12-Jan-2008] $)
  $}

  ${
    cnnvg.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The vector addition (group) operation of the normed complex vector space
       of complex numbers. $)
    cnnvg $p |- + = ( +v ` U ) $=
      ( cpv cfv c1st caddc cmul cop eqid vafval cabs fveq2i opex op1st eqtr
      addex 3eqtrr ) ACDZAEDZEDFGHZEDFARRIJSTESTKHZEDTAUAEBLTKFGMNOLFGPNQ $.
      $( [12-Jan-2008] $) $( [12-Jan-2008] $)
  $}

  ${
    cnnvba.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The base set of the normed complex vector space of complex numbers. $)
    cnnvba $p |- CC = ( Base ` U ) $=
      ( caddc crn cpv cfv cc cba cnnvg rneqi cabl wcel cgr cnaddabl ablgrp
      ax-mp axaddopr grprnOLD eqid bafval 3eqtr4 ) CDAEFZDGAHFZCUBABIJCGCKLCMLNCOP
      QRAUBUCUCSUBSTUA $.
      $( [12-Jan-2008] $) $( [7-Nov-2007] $)
  $}

  $( Derive the associative law for complex number addition ~ axaddass to
     demonstrate the use of ~ cnnv , ~ cnnvg , and ~ cnnvba . $)
  cnnvdemo $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) -> ( ( A + B ) + C ) =
      ( A + ( B + C ) ) ) $=
    ( caddc cmul cop cabs cnv wcel cc w3a co wceq eqid cnnv cnnvba cnnvg nvass
    mpan ) DEFGFZHIAJIBJICJIKABDLCDLABCDLDLMTTNZOABCTDJTUAPTUAQRS $.
    $( [12-Jan-2008] $) $( [12-Jan-2008] $)

  ${
    cnnvs.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The scalar product operation of the normed complex vector space of
       complex numbers. $)
    cnnvs $p |- x. = ( .s ` U ) $=
      ( cns cfv c1st c2nd caddc cmul cop eqid smfval cabs fveq2i opex op1st
      eqtr addex mulex op2nd 3eqtrr ) ACDZAEDZFDGHIZFDHUAAUAJKUBUCFUBUCLIZEDUCA
      UDEBMUCLGHNOPMGHQRST $.
      $( [13-Jan-2008] $) $( [12-Jan-2008] $)
  $}

  ${
    cnnvnm.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The norm operation of the normed complex vector space of complex
       numbers. $)
    cnnvnm $p |- abs = ( norm ` U ) $=
      ( cnm cfv c2nd caddc cmul cop cabs eqid nmfval fveq2i opex cc cr wf cvv
      wcel absf axcnex fex mp2an op2nd 3eqtrr ) ACDZAEDFGHZIHZEDIAUEUEJKAUGEBLU
      FIFGMNOIPNQRIQRSTNOQIUAUBUCUD $.
      $( [15-Jan-2008] $) $( [12-Jan-2008] $)
  $}

  ${
    $d x y z U $.
    cnnvm.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The vector subtraction operation of the normed complex vector space
       of complex numbers. $)
    cnnvm $p |- - = ( -v ` U ) $=
      ( vx vy vz cv cc wcel wa cmin co wceq copab2 c1 cneg cmul caddc cnsb cfv
      mulm1t adantl opreq2d negsubt eqtr2d eqeq2d pm5.32i oprabbii cxp wfn wf
      subopr ffn ax-mp fnoprval mpbi cnv cnnv cnnvba cnnvg cnnvs eqid nvmfval
      3eqtr4 ) CFZGHZDFZGHZIZEFZVDVFJKZLZIZCDEMZVHVIVDNOVFPKZQKZLZIZCDEMZJARSZV
      LVQCDEVHVKVPVHVJVOVIVHVOVDVFOZQKVJVHVNVTVDQVGVNVTLVEVFTUAUBVDVFUCUDUEUFUG
      JGGUHZUIZJVMLWAGJUJWBUKWAGJULUMCDEGGJUNUOAUPHVSVRLABUQCDEPAQVSGABURABUSAB
      UTVSVAVBUMVC $.
      $( [15-Jan-2008] $) $( [12-Jan-2008] $)
  $}

  ${
    $d x y z $.
    $( The set of complex numbers is a normed complex vector space.  The vector
       operation is ` + ` , the scalar product is ` x. ` , and the norm
       function is ` abs ` .  (Contributed by Steve Rodriguez, 3-Dec-2006.) $)
    cnnvOLD $p |- <. <. + , x. >. , abs >. e. NrmCVec $=
      ( vx vy cmul caddc cop cabs cc cc0 addex mulex cabl wcel cgr cnaddabl
      ablgrp ax-mp axaddopr grprnOLD cgi cfv cnidOLD eqcomi cnvc absf cv wceq
      abs00t biimpa absmult abstrit eqid isnvi ) ABCDCEFEZDFGHIJDGDKLDMLNDOPQRD
      STHUAUBUCUDAUEZGLUNFTHUFUNHUFUNUGUHBUEZUNUIUNUOUJUMUKUL $.
      $( [4-Dec-2006] $) $( [23-Nov-2006] $)
  $}

  ${
    elimnv.1 $e |- X = ( Base ` U ) $.
    elimnv.5 $e |- Z = ( 0v ` U ) $.
    elimnv.9 $e |- U e. NrmCVec $.
    $( Hypothesis elimination lemma for normed complex vector spaces to assist
       weak deduction theorem. $)
    elimnv $p |- if ( A e. X , A , Z ) e. X $=
      ( cnv wcel nvzcl ax-mp elimel ) ADCBHIDCIGBCDEFJKL $.
      $( [1-Nov-2007] $) $( [16-May-2007] $)
  $}

  $( Hypothesis elimination lemma for normed complex vector spaces
     to assist weak deduction theorem. $)
  elimnvu $p |- if ( U e. NrmCVec , U , <. <. + , x. >. , abs >. )
                 e. NrmCVec $=
    ( caddc cmul cop cabs cnv eqid cnnv elimel ) ABCDEDZFJJGHI $.
    $( [31-May-2007] $) $( [16-May-2007] $)

