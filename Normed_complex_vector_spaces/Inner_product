$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                            Inner product
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c .i $.

  $( Extend class notation with the class inner product functions. $)
  cip $a class .i $.

  ${
    $d k n p w x y z $.
    $( Define a function that maps a complex normed vector space to its inner
       product operation in case its norm satisfies the parallelogram identity
       (otherwise the operation is still defined, but not meaningful).  Based
       on Exercise 4(a) of [ReedSimon] p. 63 and Theorem 6.44 of [Ponnusamy]
       p. 361.  Vector addition is ` ( 1st `` w ) ` , the scalar product is
       ` ( 2nd `` w ) ` , and the norm is ` n ` . $)
    df-ip $a |- .i = ( u e. NrmCVec |->
      ( x e. ( BaseSet ` u ) , y e. ( BaseSet ` u ) |->
    ( sum_ k e. ( 1 ... 4 ) ( ( _i ^ k ) x. ( ( ( norm ` u ) ` ( x ( +v ` u )
        ( ( _i ^ k ) ( .s ` u ) y ) ) ) ^ 2 ) ) / 4 ) ) ) $.

$(
    df-ip $a |- .i = { <. x , y >. | ( x e. NrmCVec /\ E. g E. s E. n
            ( x = <. <. g , s >. , n >. /\ y = { <. <. a , b >. , c >. |
            ( ( a e. dom n /\ b e. dom n ) /\ c = ( sum_ k e. ( 1 ... 4 )
             ( ( _i ^ k ) x. ( ( n ` ( a g ( ( _i ^ k ) s b ) ) ) ^ 2 ) )
              / 4 ) ) } ) ) } $.

    df-ip $a |- .i = { <. x , y >. | ( x e. NrmCVec /\ E. g E. s E. n
            ( x = <. <. g , s >. , n >. /\ y = { <. <. a , b >. , c >. |
            ( ( a e. dom n /\ b e. dom n ) /\ c =
                ( ( ( ( ( n ` ( a g b ) ) ^ 2 ) +
                ( ( n ` ( a g ( -u 1 s b ) ) ) ^ 2 ) ) +
                ( _i x. ( ( ( n ` ( a g ( _i s b ) ) ) ^ 2 ) +
                ( ( n ` ( a g ( -u _i s b ) ) ) ^ 2 ) ) ) ) / 4 ) ) } ) ) } $.
$)
  $}

  ${
    $d k A $.  $d k B $.  $d k G $.  $d k N $.  $d k S $.
    ipval2lem.1 $e |- M e. _V $.
    $( Lemma for ~ ipval3 . $)
    ipval2lem1 $p |- [_ M / k ]_ ( ( _i ^ k ) x.
                           ( ( N ` ( A G ( ( _i ^ k ) S B ) ) ) ^ 2 ) ) =
              ( ( _i ^ M ) x. ( ( N ` ( A G ( ( _i ^ M ) S B ) ) ) ^ 2 ) ) $=
      ( ci cexp co cfv c2 cmul csb cvv wceq ax-mp csbopr2g eqtri wcel csbopr12g
      cv csbvarg opreq2i csbopr1g csbfv2g opreq1i fveq2i opreq12i ) DFIDUCZJKZA
      ULBCKZEKZGLZMJKZNKOZDFULOZDFUPOZNKZIFJKZAVABCKZEKZGLZMJKZNKFPUAZUQUTQHDFU
      LUPPNUBRURVAUSVENURIDFUKOZJKZVAVFURVHQHDFIUKPJSRVGFIJVFVGFQHDFPUDRUETZUSD
      FUOOZMJKZVEVFUSVKQHDFUOMPJUFRVJVDMJVJDFUNOZGLZVDVFVJVMQHDFUNPGUGRVLVCGVLA
      DFUMOZEKZVCVFVLVOQHDFAUMPESRVNVBAEVNURBCKZVBVFVNVPQHDFULBPCUFRURVABCVIUHT
      UETUITUHTUJT $.
      $( [31-Jan-2007] $)
  $}

  ${
    $d k u x y G $.  $d k u x y N $.  $d k u x y S $.  $d k u x y U $.
    $d k x y A $.  $d k y B $.  $d u x y X $.
    ipfval.1 $e |- X = ( BaseSet ` U ) $.
    ipfval.2 $e |- G = ( +v ` U ) $.
    ipfval.4 $e |- S = ( .s ` U ) $.
    ipfval.6 $e |- N = ( norm ` U ) $.
    ipfval.7 $e |- P = ( .i ` U ) $.
    $( The inner product function on a normed complex vector space.  The
       definition is meaningful for vector spaces that are also inner product
       spaces, i.e. satisfy the parallelogram law. $)
    ipfval $p |- ( U e. NrmCVec -> P = ( x e. X , y e. X |->
      ( sum_ k e. ( 1 ... 4 ) ( ( _i ^ k ) x.
        ( ( N ` ( x G ( ( _i ^ k ) S y ) ) ) ^ 2 ) ) / 4 ) ) ) $=
      ( cfv c4 co cv cexp cvv vu cnv wcel cip c1 cfz ci cmul csu cdiv cmpt2 cba
      c2 cns cpv cnm wceq fveq2 syl6eqr eqidd opreq123d fveq12d opreq1d opreq2d
      opreqd sumeq2sdv mpt2eq123dv df-ip eqeltri mpt2exg mp2an fvmpt syl5eq
      fvex ) EUBUCCEUDOABIIUEPUFQZUGFRSQZARZVPBRZDQZGQZHOZUMSQZUHQZFUIZPUJQZUKZ
      NUAEABUARZULOZWHVOVPVQVPVRWGUNOZQZWGUOOZQZWGUPOZOZUMSQZUHQZFUIZPUJQZUKWFU
      BUDWGEUQZABWHWHWRIIWEWSWHEULOZIWGEULURJUSZXAWSWQWDPUJWSVOWPWCFWSWOWBVPUHW
      SWNWAUMSWSWLVTWMHWSWMEUPOHWGEUPURMUSWSVQVQWJVSWKGWSWKEUOOGWGEUOURKUSWSVQU
      TWSWIDVPVRWSWIEUNODWGEUNURLUSVEVAVBVCVDVFVCVGABUAFVHITUCZXBWFTUCIWTTJEULV
      NVIZXCABIIWETTVJVKVLVM $.
      $( [16-Nov-2013] $) $( [10-Apr-2007] $)

    $( Value of the inner product.  The definition is meaningful for normed
       complex vector spaces that are also inner product spaces, i.e. satisfy
       the parallelogram law, although for convenience we define it for any
       normed complex vector space.  The vector (group) addition operation is
       ` G ` , the scalar product is ` S ` , the norm is ` N ` , and the set of
       vectors is ` X ` .  Equation 6.45 of [Ponnusamy] p. 361. $)
    ipval $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B ) =
                ( sum_ k e. ( 1 ... 4 ) ( ( _i ^ k ) x. ( ( N ` ( A G
                    ( ( _i ^ k ) S B ) ) ) ^ 2 ) ) / 4 ) ) $=
      ( co c4 cexp c2 cmul cdiv vx vy cnv wcel c1 cfz ci cv cfv csu wceq ipfval
      wa cmpt2 opreqd opreq1 fveq2d opreq1d opreq2d sumeq2sdv opreq2 eqid oprex
      ovmpt2 sylan9eq 3impb ) EUCUDZAIUDZBIUDZABCOZUEPUFOZUGFUHQOZAVLBDOZGOZHUI
      ZRQOZSOZFUJZPTOZUKVGVHVIUMVJABUAUBIIVKVLUAUHZVLUBUHZDOZGOZHUIZRQOZSOZFUJZ
      PTOZUNZOVSVGCWIABUAUBCDEFGHIJKLMNULUOUAUBABIIWHVSWIVKVLAWBGOZHUIZRQOZSOZF
      UJZPTOVTAUKZWGWNPTWOVKWFWMFWOWEWLVLSWOWDWKRQWOWCWJHVTAWBGUPUQURUSUTURWABU
      KZWNVRPTWPVKWMVQFWPWLVPVLSWPWKVORQWPWJVNHWPWBVMAGWABVLDVAUSUQURUSUTURWIVB
      VRPTVCVDVEVF $.
      $( [16-Nov-2013] $) $( [31-Jan-2007] $)

    $( Lemma for ~ ipval3 . $)
    ipval2lem2 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
               ( ( N ` ( A G ( C S B ) ) ) ^ 2 ) e. RR ) $=
      ( cnv wcel w3a co cr adantr cc wa cfv cexp simp1 simp2 nvscl 3com23 3expa
      c2 3adantl2 nvgcl syl111anc nvcl syl11anc resqcl syl ) FOPZAIPZBIPZQZCUAP
      ZUBZACBERZGRZHUCZSPZVFUJUDRSPVCURVEIPZVGVAURVBURUSUTUETZVCURUSVDIPZVHVIVA
      USVBURUSUTUFTURUTVBVJUSURUTVBVJURVBUTVJCBEFIJLUGUHUIUKAVDFGIJKULUMVEFHIJM
      UNUOVFUPUQ $.
      $( [1-Oct-2007] $) $( [1-Feb-2007] $)

    $( Lemma for ~ ipval3 . $)
    ipval2lem3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( ( N ` ( A G B ) ) ^ 2 ) e. RR ) $=
      ( wcel c1 co cfv c2 cexp cr cnv w3a wceq wa nvsid opreq2d opreq1d 3adant2
      fveq2d cc ax-1cn ipval2lem2 mpan2 eqeltrrd ) EUANZAHNZBHNZUBZAOBDPZFPZGQZ
      RSPZABFPZGQZRSPZTUOUQVBVEUCUPUOUQUDZVAVDRSVFUTVCGVFUSBAFBDEHIKUEUFUIUGUHU
      ROUJNVBTNUKABOCDEFGHIJKLMULUMUN $.
      $( [1-Oct-2007] $) $( [1-Feb-2007] $)

    $( Lemma for ~ ipval3 . $)
    ipval2lem4 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
               ( ( N ` ( A G ( C S B ) ) ) ^ 2 ) e. CC ) $=
      ( cnv wcel w3a cc wa co cfv c2 cexp ipval2lem2 recnd ) FOPAIPBIPQCRPSACBE
      TGTHUAUBUCTABCDEFGHIJKLMNUDUE $.
      $( [1-Oct-2007] $) $( [1-Feb-2007] $)

    $( Expansion of the inner product value ~ ipval . _Warning:  The HTML proof
       page is 0.5MB in size_. $)
    ipval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B )
               = ( ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) +
               ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u _i S B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
      ( vk co c1 ci c2 caddc wceq cnv wcel w3a c4 cfz cv cexp cfv cmul csu cdiv
      cneg cmin ipval cc ax-icn ipval2lem4 mpan2 mulcl mpan ax-1cn negcli subcl
      syl11anc negsub mulm1 opreq2d eqtrd mulneg1 opreq12d subdi mp3an1 opreq1d
      syl sub32 syl111anc 3eqtr4d wa i4 a1i opreq1i nvsid syl5eq fveq2d 3adant2
      ipval2lem3 recnd mulid2 c3 csb 3re recni addcomi eqtri opreq2i sumeq1i cz
      df-4 1z oprex fsum4 ax-mp elisseti ipval2lem1 exp1 fveq2i opreq12i csbeq1
      2cn df-2 i2 3eqtr3i cr df-3 i3 4re eqtr3i 3eqtri addcom subadd23 eqtr4d )
      EUAUBZAHUBZBHUBZUCZABCOPUDUEOZQNUFUGOZAYGBDOFOGUHRUGOZUIOZNUJZUDUKOABFOZG
      UHZRUGOZAPULZBDOZFOZGUHZRUGOZUMOZQAQBDOZFOZGUHZRUGOZAQULZBDOZFOZGUHZRUGOZ
      UMOZUIOZSOZUDUKOABCDENFGHIJKLMUNYEYJUUKUDUKYEQUUCUIOZYNYRUIOZSOZUUDUUHUIO
      ZSOZQUDUGOZAUUQBDOZFOZGUHZRUGOZUIOZSOZUUJYRUMOZYMSOZYJUUKYEUUPUVDUVBYMSYE
      UULYRUMOZQUUHUIOZULZSOZUVFUVGUMOZUUPUVDYEUVFUOUBZUVGUOUBZUVIUVJTYEUULUOUB
      ZYRUOUBZUVKYEUUCUOUBZUVMYEQUOUBZUVOUPABQCDEFGHIJKLMUQURZUVPUVOUVMUPQUUCUS
      UTVNZYEYNUOUBUVNPVAVBABYNCDEFGHIJKLMUQURZUULYRVCVDYEUUHUOUBZUVLYEUUDUOUBU
      VTQUPVBABUUDCDEFGHIJKLMUQURZUVPUVTUVLUPQUUHUSUTVNZUVFUVGVEVDYEUUNUVFUUOUV
      HSYEUUNUULYRULZSOZUVFYEUUMUWCUULSYEUVNUUMUWCTUVSYRVFVNVGYEUVMUVNUWDUVFTUV
      RUVSUULYRVEVDVHYEUVTUUOUVHTZUWAUVPUVTUWEUPQUUHVIUTVNVJYEUVDUULUVGUMOZYRUM
      OZUVJYEUUJUWFYRUMYEUVOUVTUUJUWFTZUVQUWAUVPUVOUVTUWHUPQUUCUUHVKVLVDVMYEUVM
      UVLUVNUWGUVJTUVRUWBUVSUULUVGYRVOVPVHVQYEUVBPYMUIOZYMYBYDUVBUWITYCYBYDVRZU
      UQPUVAYMUIUUQPTUWJVSVTUWJUUTYLRUGUWJUUSYKGUWJUURBAFUWJUURPBDOBUUQPBDVSWAB
      DEHIKWBWCVGWDVMVJWEYEYMUOUBZUWIYMTYEYMABCDEFGHIJKLMWFWGZYMWHVNVHVJYJUVCTY
      EYJPPWISOZUEOZYINUJZNPYIWJZNPPSOZYIWJZSOZNPRSOZYIWJZSOZNUWMYIWJZSOZUVCYFU
      WNYINUDUWMPUEUDWIPSOUWMWRWIPWIWKWLVAWMWNZWOWPPWQUBUWOUXDTWSYINPYGYHUIWTXA
      XBUXBUUPUXCUVBSUWSUUNUXAUUOSUWPUULUWRUUMSUWPQPUGOZAUXFBDOZFOZGUHZRUGOZUIO
      UULABDNFPGPWQWSXCXDUXFQUXJUUCUIUVPUXFQTUPQXEXBZUXIUUBRUGUXHUUAGUXGYTAFUXF
      QBDUXKWAWOXFWAXGWNNRYIWJZQRUGOZAUXMBDOZFOZGUHZRUGOZUIOUWRUUMABDNFRGRUOXIX
      CXDRUWQTUXLUWRTXJNRUWQYIXHXBUXMYNUXQYRUIXKUXPYQRUGUXOYPGUXNYOAFUXMYNBDXKW
      AWOXFWAXGXLXGNWIYIWJZQWIUGOZAUXSBDOZFOZGUHZRUGOZUIOUXAUUOABDNFWIGWIXMWKXC
      XDWIUWTTUXRUXATWIRPSOUWTXNRPXIVAWMWNNWIUWTYIXHXBUXSUUDUYCUUHUIXOUYBUUGRUG
      UYAUUFGUXTUUEAFUXSUUDBDXOWAWOXFWAXGXLXGNUDYIWJZUXCUVBUDUWMTUYDUXCTUXENUDU
      WMYIXHXBABDNFUDGUDXMXPXCXDXQXGXRVTYEUUKUUJYSSOZUVEYEYSUOUBZUUJUOUBZUUKUYE
      TYEUWKUVNUYFUWLUVSYMYRVCVDYEUUIUOUBZUYGYEUVOUVTUYHUVQUWAUUCUUHVCVDUVPUYHU
      YGUPQUUIUSUTVNZYSUUJXSVDYEUYGUVNUWKUVEUYETUYIUVSUWLUUJYRYMXTVPYAVQVMVH $.
      $( [1-Oct-2007] $) $( [31-Jan-2007] $)

    $( Four times the inner product value ~ ipval3 , useful for simplifying
       certain proofs. $)
    4ipval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
        ( 4 x. ( A P B ) ) = ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) +
               ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u _i S B ) ) ) ^ 2 ) ) ) ) ) $=
      ( wcel c4 co ci cc syl11anc syl cnv w3a cmul c2 cexp cneg cmin caddc cdiv
      c1 ipval2 opreq2d wceq cr simp1 nvgcl nvcl recnd sqcl ax-1cn negcli nvscl
      cfv mp3an2 3adant2 syld3an3 subcl mulcl mpan addcl cc0 wne 4re recni 4pos
      ax-icn gt0ne0ii divcan2 mp3an23 eqtrd ) EUANZAHNZBHNZUBZOABCPZUCPOABFPZGV
      CZUDUEPZAUJUFZBDPZFPZGVCZUDUEPZUGPZQAQBDPZFPZGVCZUDUEPZAQUFZBDPZFPZGVCZUD
      UEPZUGPZUCPZUHPZOUIPZUCPZXFWDWEXGOUCABCDEFGHIJKLMUKULWDXFRNZXHXFUMZWDWNRN
      ZXERNZXIWDWHRNZWMRNZXKWDWGRNXMWDWGWDWAWFHNWGUNNWAWBWCUOZABEFHIJUPWFEGHILU
      QSURWGUSTWDWLRNXNWDWLWDWAWKHNZWLUNNXOWAWBWCWJHNZXPWAWCXQWBWAWIRNWCXQUJUTV
      AWIBDEHIKVBVDVEAWJEFHIJUPVFWKEGHILUQSURWLUSTWHWMVGSWDXDRNZXLWDWRRNZXCRNZX
      RWDWQRNXSWDWQWDWAWPHNZWQUNNXOWAWBWCWOHNZYAWAWCYBWBWAQRNZWCYBVPQBDEHIKVBVD
      VEAWOEFHIJUPVFWPEGHILUQSURWQUSTWDXBRNXTWDXBWDWAXAHNZXBUNNXOWAWBWCWTHNZYDW
      AWCYEWBWAWSRNWCYEQVPVAWSBDEHIKVBVDVEAWTEFHIJUPVFXAEGHILUQSURXBUSTWRXCVGSY
      CXRXLVPQXDVHVITWNXEVJSXIORNOVKVLXJOVMVNOVMVOVQXFOVRVSTVT $.
      $( [10-Apr-2007] $)

    ${
      ipval3.3 $e |- M = ( -v ` U ) $.
      $( Expansion of the inner product value ~ ipval . $)
      ipval3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B )
               = ( ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A M B ) ) ^ 2 ) ) +
               ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A M ( _i S B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
        ( wcel co c2 cexp ci cnv w3a cfv c1 cneg cmin cmul caddc c4 cdiv ipval2
        nvmval fveq2d opreq1d opreq2d wceq ax-icn nvscl mp3an2 3adant2 syld3an3
        cc wa mulm1i opreq1i ax-1cn negcli nvsass mp3anr1 mpanr1 syl5reqr eqtrd
        opreq12d eqtr4d ) EUAPZAIPZBIPZUBZABCQABFQHUCRSQZAUDUEZBDQFQZHUCZRSQZUF
        QZTATBDQZFQHUCRSQZATUEZBDQZFQZHUCZRSQZUFQZUGQZUHQZUIUJQVSABGQZHUCZRSQZU
        FQZTWFAWEGQZHUCZRSQZUFQZUGQZUHQZUIUJQABCDEFHIJKLMNUKVRXDWNUIUJVRWRWDXCW
        MUHVRWQWCVSUFVRWPWBRSVRWOWAHABDEFGIJKLOULUMUNUOVRXBWLTUGVRXAWKWFUFVRWTW
        JRSVRWSWIHVRWSAVTWEDQZFQZWIVOVPVQWEIPZWSXFUPVOVQXGVPVOTVBPZVQXGUQTBDEIJ
        LURUSUTAWEDEFGIJKLOULVAVRXEWHAFVOVQXEWHUPVPVOVQVCWHVTTUGQZBDQZXEXIWGBDT
        UQVDVEVOXHVQXJXEUPZUQVOVTVBPXHVQXKUDVFVGVTTBDEIJLVHVIVJVKUTUOVLUMUNUOUO
        VMUNVN $.
        $( [17-Nov-2007] $)

      $( Lemma for ~ ipval3 . $)
      ipval2lem5 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
                 ( ( N ` ( A M ( C S B ) ) ) ^ 2 ) e. RR ) $=
        ( wcel co cr adantr cnv w3a cc wa cfv c2 simp1 simp2 nvscl 3com23 3expa
        cexp 3adantl2 nvmcl syl111anc nvcl syl11anc resqcl syl ) FUAQZAJQZBJQZU
        BZCUCQZUDZACBERZHRZIUEZSQZVHUFULRSQVEUTVGJQZVIVCUTVDUTVAVBUGTZVEUTVAVFJ
        QZVJVKVCVAVDUTVAVBUHTUTVBVDVLVAUTVBVDVLUTVDVBVLCBEFJKMUIUJUKUMAVFFHJKPU
        NUOVGFIJKNUPUQVHURUS $.
        $( [1-Feb-2008] $)

      $( Lemma for ~ ipval3 . $)
      ipval2lem6 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                 ( ( N ` ( A M B ) ) ^ 2 ) e. RR ) $=
        ( wcel c1 co c2 cexp cnv w3a cfv cr wceq opreq2d fveq2d opreq1d 3adant2
        wa nvsid cc ax-1cn ipval2lem5 mpan2 eqeltrrd ) EUAPZAIPZBIPZUBZAQBDRZGR
        ZHUCZSTRZABGRZHUCZSTRZUDUQUSVDVGUEURUQUSUJZVCVFSTVHVBVEHVHVABAGBDEIJLUK
        UFUGUHUIUTQULPVDUDPUMABQCDEFGHIJKLMNOUNUOUP $.
        $( [1-Feb-2008] $)

      $( Four times the inner product value ~ ipval3 , useful for simplifying
         certain proofs. $)
      4ipval3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
          ( 4 x. ( A P B ) ) = ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
                 ( ( N ` ( A M B ) ) ^ 2 ) ) +
                 ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
                 ( ( N ` ( A M ( _i S B ) ) ) ^ 2 ) ) ) ) ) $=
        ( wcel c4 co ci cc cnv w3a cmul cfv cexp cmin caddc cdiv ipval3 opreq2d
        c2 wceq ipval2lem3 recnd ipval2lem6 subcl syl11anc cr ax-icn ipval2lem2
        mpan2 ipval2lem5 mulcl mpan syl addcl cc0 wne 4re 4pos gt0ne0ii divcan2
        recni mp3an23 eqtrd ) EUAPAIPBIPUBZQABCRZUCRQABFRHUDUKUERZABGRHUDUKUERZ
        UFRZSASBDRZFRHUDUKUERZAWAGRHUDUKUERZUFRZUCRZUGRZQUHRZUCRZWFVPVQWGQUCABC
        DEFGHIJKLMNOUIUJVPWFTPZWHWFULZVPVTTPZWETPZWIVPVRTPVSTPWKVPVRABCDEFHIJKL
        MNUMUNVPVSABCDEFGHIJKLMNOUOUNVRVSUPUQVPWDTPZWLVPWBTPWCTPWMVPWBVPSTPZWBU
        RPUSABSCDEFHIJKLMNUTVAUNVPWCVPWNWCURPUSABSCDEFGHIJKLMNOVBVAUNWBWCUPUQWN
        WMWLUSSWDVCVDVEVTWEVFUQWIQTPQVGVHWJQVIVMQVIVJVKWFQVLVNVEVO $.
        $( [1-Feb-2008] $)
    $}
  $}

  ${
    $d k n p w x y z G $.  $d k n p w x y z N $.  $d k n p w x y z S $.
    $d k n p w x y z U $.  $d k x y z A $.  $d k x y z B $.  $d x y z X $.
    ipid.1 $e |- X = ( BaseSet ` U ) $.
    ipid.6 $e |- N = ( norm ` U ) $.
    ipid.7 $e |- P = ( .i ` U ) $.
    $( The inner product of a vector with itself is the square of the vector's
       norm.  Equation I4 of [Ponnusamy] p. 362. $)
    ipidsq $p |- ( ( U e. NrmCVec /\ A e. X ) ->
                ( A P A ) = ( ( N ` A ) ^ 2 ) ) $=
      ( wcel co cfv c2 cexp c1 ci cmul caddc c4 wceq cc0 cnv cpv cneg cmin cdiv
      wa cns eqid ipval2 3anidm23 c1st cvc nvvc vafval smfval bafval vc2 fveq2d
      sylan cr cle wbr 2nn nnrei 0re 2re 2pos ltleii pm3.2i nvsge0 mp3an2 eqtrd
      opreq1d nvcl recnd cn0 2cn 2nn0 mulexp mp3an13 syl sq2 opreq1i a1i 3eqtrd
      cn0v nvrinv nvz0 adantr sq0 syl6eq opreq12d 4re recni sqcl sylancr subid1
      mulcl cabs csqr 1re renegcli absreim mp2an ax-icn ax-1cn mulneg2i mulid1i
      negeqi eqtri opreq2i fveq2i sqneg ax-mp 3eqtr3i 3eqtr2i negcli addcli nvs
      3eqtr4a nvdir mp3anr1 mpanr1 nvsid 3eqtr3d opreq2d ipval2lem4 mpan2 subid
      cc w3a mul01i addid1 eqtr2d wne 4pos gt0ne0ii divcan3 mp3an23 3eqtr2d ) C
      UAIZAEIZUFZAABJZAACUBKZJZDKZLMJZANUCZACUGKZJUUEJZDKZLMJZUDJZOAOAUUJJZUUEJ
      ZDKZLMJZAOUCZAUUJJZUUEJZDKZLMJZUDJZPJZQJZRUEJZRADKZLMJZPJZRUEJZUVIUUAUUBU
      UDUVGSAABUUJCUUEDEFUUEUHZUUJUHZGHUIUJUUCUVJUVFRUEUUCUVFUVJTQJZUVJUUCUUNUV
      JUVETQUUCUUNUVJTUDJZUVJUUCUUHUVJUUMTUDUUCUUHLUVHPJZLMJZLLMJZUVIPJZUVJUUCU
      UGUVPLMUUCUUGLAUUJJZDKZUVPUUCUUFUVTDUUACUKKZULIUUBUUFUVTSCUWBUWBUHUMAUUJU
      UEUWBECUUEUVLUNUUJCUVMUOCUUEEFUVLUPUQUSURUUALUTIZTLVAVBZUFUUBUWAUVPSUWCUW
      DLVCVDTLVEVFVGVHVILAUUJCDEFUVMGVJVKVLVMUUCUVHYJIZUVQUVSSZUUCUVHACDEFGVNVO
      ZLYJIUWELVPIUWFVQVRLUVHLVSVTWAUVSUVJSUUCUVRRUVIPWBWCWDWEUUCUUMTLMJTUUCUUL
      TLMUUCUULCWFKZDKZTUUCUUKUWHDAUUJCUUEEUWHFUVLUVMUWHUHZWGURUUAUWITSUUBCDUWH
      UWJGWHWIVLVMWJWKWLUUCUVJYJIZUVOUVJSUUCRYJIZUVIYJIZUWKRWMWNZUUCUWEUWMUWGUV
      HWOWAZRUVIWRWPZUVJWQWAVLUUCUVEOTPJTUUCUVDTOPUUCUVDUURUURUDJZTUUCUVCUURUUR
      UDUUCUVBUUQLMUUCNUUSQJZAUUJJZDKZNOQJZAUUJJZDKZUVBUUQUUCUWRWSKZUVHPJZUXAWS
      KZUVHPJZUWTUXCUXDUXFUVHPUXDNLMJZUXHQJZWTKZNONPJZQJZWSKZUXFNOUUIPJZQJZWSKZ
      UXHUUILMJZQJZWTKZUXDUXJNUTIZUUIUTIUXPUXSSXANXAXBNUUIXCXDUXOUWRWSUXNUUSNQU
      XNUXKUCUUSONXEXFXGUXKOOXEXHZXIXJXKXLUXRUXIWTUXQUXHUXHQNYJIZUXQUXHSXFNXMXN
      XKXLXOUXTUXTUXMUXJSXAXANNXCXDUXLUXAWSUXKONQUYAXKXLXPWCUUAUWRYJIUUBUWTUXES
      NUUSXFOXEXQZXRUWRAUUJCDEFUVMGXSVKUUAUXAYJIUUBUXCUXGSNOXFXEXRUXAAUUJCDEFUV
      MGXSVKXTUUCUWSUVADUUCUWSNAUUJJZUUTUUEJZUVAUUAUUSYJIZUUBUWSUYESZUYCUUAUYBU
      YFUUBUYGXFNUUSAUUJCUUEEFUVLUVMYAYBYCUUCUYDAUUTUUEAUUJCEFUVMYDZVMVLURUUCUX
      BUUPDUUCUXBUYDUUOUUEJZUUPUUAOYJIZUUBUXBUYISZXEUUAUYBUYJUUBUYKXFNOAUUJCUUE
      EFUVLUVMYAYBYCUUCUYDAUUOUUEUYHVMVLURYEVMYFUUCUURYJIZUWQTSUUAUUBUYLUUAUUBU
      UBYKUYJUYLXEAAOBUUJCUUEDEFUVLUVMGHYGYHUJUURYIWAVLYFOXEYLWKWLUUCUWKUVNUVJS
      UWPUVJYMWAYNVMUUCUWMUVKUVISZUWOUWMUWLRTYOUYMUWNRWMYPYQUVIRYRYSWAYT $.
      $( [27-Nov-2007] $) $( [1-Feb-2007] $)

    $( Norm expressed in terms of inner product. $)
    ipnm $p |- ( ( U e. NrmCVec /\ A e. X ) ->
                ( N ` A ) = ( sqr ` ( A P A ) ) ) $=
      ( cnv wcel wa co csqr cfv c2 cexp ipidsq fveq2d cr cc0 cle wbr wceq nvge0
      nvcl sqrsq syl11anc eqtr2d ) CIJAEJKZAABLZMNADNZOPLZMNZUKUIUJULMABCDEFGHQ
      RUIUKSJTUKUAUBUMUKUCACDEFGUEACDEFGUDUKUFUGUH $.
      $( [11-Sep-2007] $)
  $}

  ${
    $d k A $.  $d k B $.  $d k G $.  $d k x y z U $.  $d k W $.
    $d x y z k X $.
    ipcl.1 $e |- X = ( BaseSet ` U ) $.
    ipcl.7 $e |- P = ( .i ` U ) $.
    $( An inner product is a complex number. $)
    ipcl $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( A P B ) e. CC ) $=
      ( vk wcel co c1 c4 ci cexp cfv cc eqid cn mpan 3syl cnv w3a cfz cv cns c2
      cpv cnm cmul csu cdiv ipval wral cn0 elfznn nnnn0 ax-icn expcl ipval2lem4
      wa adantl sylan2 mulcl syl11anc ralrimiva cuz cz cle wbr eluz1i 4nn ax-mp
      1z nnz cmin clt cc0 ax-1cn subidi 4pos eqbrtri wb nnlem1lt mp2an mpbir2an
      1nn mpbir fsumcl wne 4re recni gt0ne0ii divcl mp3an23 eqeltrd ) DUAIAEIBE
      IUBZABCJKLUCJZMHUDZNJZAWSBDUEOZJDUGOZJDUHOZOUFNJZUIJZHUJZLUKJZPABCWTDHXAX
      BEFXAQZWTQZXBQZGULWPXDPIZHWQUMZXEPIZXFPIZWPXJHWQWPWRWQIZUTWSPIZXCPIZXJXNX
      OWPXNWRRIWRUNIZXOWRLUOWRUPMPIXQXOUQMWRURSTZVAXNWPXOXPXRABWSCWTDXAXBEFXGXH
      XIGUSVBWSXCVCVDVELKVFOIZXKXLXSLVGIZKLVHVIZKLVMVJLRIZXTVKLVNVLYAKKVOJZLVPV
      IZYCVQLVPKVRVSVTWAKRIYBYAYDWBWFVKKLWCWDWGWEXDHKLWHSXLLPILVQWIXMLWJWKLWJVT
      WLXELWMWNTWO $.
      $( [6-Oct-2007] $) $( [1-Feb-2007] $)

    $( Mapping for the inner product operation. $)
    ipf $p |- ( U e. NrmCVec -> P : ( X X. X ) --> CC ) $=
      ( vx vy vk cnv wcel cxp cc wf c4 co cv cexp cfv wral eqid cfz cns cpv cnm
      c1 ci c2 cmul csu cdiv cmpt2 w3a ipval eqeltrrd 3expib ralrimivv foprab2a
      ipcl sylib ipfval feq1d mpbird ) BIJZCCKZLAMVDLFGCCUENUAOUFHPQOZFPZVEGPZB
      UBRZOBUCRZOBUDRZRUGQOUHOHUINUJOZUKZMZVCVKLJZGCSFCSVMVCVNFGCCVCVFCJZVGCJZV
      NVCVOVPULVFVGAOVKLVFVGAVHBHVIVJCDVITZVHTZVJTZEUMVFVGABCDEURUNUOUPFGCCVKLV
      LVLTUQUSVCVDLAVLFGAVHBHVIVJCDVQVRVSEUTVAVB $.
      $( [16-Nov-2013] $) $( [28-Jan-2008] $)

    $( The complex conjugate of an inner product reverses its arguments.
       Equation I1 of [Ponnusamy] p. 362. $)
    ipcj $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( * ` ( A P B ) ) = ( B P A ) ) $=
      ( wcel cfv co c2 cexp cmin ci c4 cdiv cc wceq syl11anc cr cnv w3a cpv cnm
      c1 cneg cns cmul caddc ccj eqid ipval2lem3 recnd ax-1cn negcli ipval2lem4
      mpan2 subcl ax-icn mulcl mpan syl addcl cc0 wne recni 4pos gt0ne0ii cjdiv
      mp3an23 cjre ax-mp opreq2i ipval2lem2 resubcl cjreim submul2 mp3an2 nvcom
      fveq2d opreq1d nvdif opreq12d negsubdi2 nvpi 3com23 eqcomd opreq2d 3eqtrd
      4re eqtrd syl5eq ipval2 3eqtr4d ) DUAHZAEHZBEHZUBZABDUCIZJZDUDIZIZKLJZAUE
      UFZBDUGIZJWSJXAIZKLJZMJZNANBXEJWSJXAIZKLJZANUFZBXEJWSJXAIZKLJZMJZUHJZUIJZ
      OPJZUJIZBAWSJZXAIZKLJZBXDAXEJWSJXAIZKLJZMJZNBNAXEJWSJXAIZKLJZBXKAXEJWSJXA
      IZKLJZMJZUHJZUIJZOPJZABCJZUJIBACJZWRXRXPUJIZOUJIZPJZYLWRXPQHZXRYQRZWRXHQH
      ZXOQHZYRWRXCQHXGQHZYTWRXCABCXEDWSXAEFWSUKZXEUKZXAUKZGULZUMWRXDQHZUUBUEUNU
      OZABXDCXEDWSXAEFUUCUUDUUEGUPUQXCXGURSZWRXNQHZUUAWRXJQHZXMQHZUUJWRNQHZUUKU
      SABNCXEDWSXAEFUUCUUDUUEGUPUQZWRXKQHZUULNUSUOZABXKCXEDWSXAEFUUCUUDUUEGUPUQ
      ZXJXMURSZUUMUUJUUAUSNXNUTVAVBXHXOVCSYROQHOVDVEYSOWJVFOWJVGVHXPOVIVJVBWRYQ
      YOOPJYLYPOYOPOTHYPORWJOVKVLVMWRYOYKOPWRYOXHXOMJZXHNXNUFZUHJZUIJZYKWRXHTHZ
      XNTHZYOUUSRWRXCTHXGTHZUVCUUFWRUUGUVEUUHABXDCXEDWSXAEFUUCUUDUUEGVNUQXCXGVO
      SWRXJTHZXMTHZUVDWRUUMUVFUSABNCXEDWSXAEFUUCUUDUUEGVNUQWRUUOUVGUUPABXKCXEDW
      SXAEFUUCUUDUUEGVNUQXJXMVOSXHXNVPSWRYTUUJUUSUVBRZUUIUURYTUUMUUJUVHUSXHNXNV
      QVRSWRXHYDUVAYJUIWRXCYAXGYCMWRXBXTKLWRWTXSXAABDWSEFUUCVSVTWAWRXFYBKLABXED
      WSXAEFUUCUUDUUEWBWAWCWRUUTYINUHWRUUTXMXJMJZYIWRUUKUULUUTUVIRUUNUUQXJXMWDS
      WRXMYFXJYHMWRXLYEKLWRYEXLWOWQWPYEXLRBAXEDWSXAEFUUCUUDUUEWEWFWGWAWRXIYGKLA
      BXEDWSXAEFUUCUUDUUEWEWAWCWKWHWCWIWAWLWKWRYMXQUJABCXEDWSXAEFUUCUUDUUEGWMVT
      WOWQWPYNYLRBACXEDWSXAEFUUCUUDUUEGWMWFWN $.
      $( [1-Feb-2007] $)

    $( An inner product times its conjugate. $)
    ipipcj $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( ( A P B ) x. ( B P A ) ) = ( ( abs ` ( A P B ) ) ^ 2 ) ) $=
      ( cnv wcel w3a co cabs cfv c2 cexp ccj cmul cc wceq ipcl absvalsq opreq2d
      syl ipcj eqtr2d ) DHIAEIBEIJZABCKZLMNOKZUGUGPMZQKZUGBACKZQKUFUGRIUHUJSABC
      DEFGTUGUAUCUFUIUKUGQABCDEFGUDUBUE $.
      $( [23-Nov-2007] $)

    $( Orthogonality (meaning inner product is 0) is commutative. $)
    iporthcom $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X )
              -> ( ( A P B ) = 0 <-> ( B P A ) = 0 ) ) $=
      ( cnv wcel co cc0 wceq ccj cfv fveq2 cj0 syl6eq ipcj eqeq1d syl5ib 3com23
      w3a impbid ) DHIZAEIZBEIZUBZABCJZKLZBACJZKLZUIUHMNZKLUGUKUIULKMNZKUHKMOPQ
      UGULUJKABCDEFGRSTUKUJMNZKLUGUIUKUNUMKUJKMOPQUGUNUHKUDUFUEUNUHLBACDEFGRUAS
      TUC $.
      $( [17-Apr-2008] $)
  $}

  ${
    ip0r.1 $e |- X = ( BaseSet ` U ) $.
    ip0r.5 $e |- Z = ( 0vec ` U ) $.
    ip0r.7 $e |- P = ( .i ` U ) $.
    $( Inner product with a zero second argument. $)
    ip0r $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( A P Z ) = 0 ) $=
      ( wcel co cfv c2 cexp cmin ci caddc c4 cc0 wceq cc cnv wa cpv cnm c1 cneg
      cns cmul cdiv nvzcl adantr eqid ipval2 mpd3an3 ax-1cn negcli nvsz opreq2d
      mpan2 fveq2d cr ipval2lem3 recnd subid syl eqtrd ax-icn eqtr4d ipval2lem4
      opreq1d w3a opreq12d mul01i opreq2i 0cn addid1i eqtri syl6eq 4re gt0ne0ii
      recni 4pos div0i ) CUAIZADIZUBZAEBJZAECUCKZJZCUDKZKZLMJZAUEUFZECUGKZJZWHJ
      ZWJKZLMJZNJZOAOEWNJZWHJZWJKZLMJZAOUFZEWNJZWHJZWJKZLMJZNJZUHJZPJZQUIJZRWDW
      EEDIZWGXLSWDXMWECDEFGUJUKZAEBWNCWHWJDFWHULZWNULZWJULZHUMUNWFXLRQUIJRWFXKR
      QUIWFXKRORUHJZPJZRWFWSRXJXRPWFWSWLWLNJZRWFWRWLWLNWFWQWKLMWFWPWIWJWFWOEAWH
      WDWOESZWEWDWMTIYAUEUOUPWMWNCEXPGUQUSUKURUTVJURWFWLTIXTRSWFWLWDWEXMWLVAIXN
      AEBWNCWHWJDFXOXPXQHVBUNVCWLVDVEVFWFXIROUHWFXIXCXCNJZRWFXHXCXCNWFXGXBLMWFX
      FXAWJWFXEWTAWHWDXEWTSWEWDXEEWTWDXDTIXEESOVGUPXDWNCEXPGUQUSWDOTIZWTESVGOWN
      CEXPGUQUSVHUKURUTVJURWFXCTIZYBRSWDWEXMYDXNWDWEXMVKYCYDVGAEOBWNCWHWJDFXOXP
      XQHVIUSUNXCVDVEVFURVLXSRRPJRXRRRPOVGVMVNRVOVPVQVRVJQQVSWAQVSWBVTWCVRVF $.
      $( [1-Nov-2007] $) $( [5-Feb-2007] $)

    $( Inner product with a zero first argument.  Part of proof of Theorem 6.44
       of [Ponnusamy] p. 361. $)
    ip0l $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( Z P A ) = 0 ) $=
      ( cnv wcel wa co ccj cfv cc0 wceq nvzcl adantr ipcj mpd3an3 fveq2d syl6eq
      ip0r cj0 eqtr3d ) CIJZADJZKZAEBLZMNZEABLZOUFUGEDJZUJUKPUFULUGCDEFGQRAEBCD
      FHSTUHUJOMNOUHUIOMABCDEFGHUCUAUDUBUE $.
      $( [2-Nov-2007] $) $( [5-Feb-2007] $)

    $( The inner product of a vector with itself is zero iff the vector is
       zero.  Part of Definition 3.1-1 of [Kreyszig] p. 129. $)
    ipz $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( ( A P A ) = 0 <-> A = Z ) ) $=
      ( cnv wcel wa co cc0 wceq cnm cfv c2 cexp eqid ipidsq cc nvcl recnd sqeq0
      eqeq1d wb syl nvz 3bitrd ) CIJADJKZAABLZMNACOPZPZQRLZMNZUMMNZAENUJUKUNMAB
      CULDFULSZHTUEUJUMUAJUOUPUFUJUMACULDFUQUBUCUMUDUGACULDEFGUQUHUI $.
      $( [24-Jan-2008] $)
  $}

  ${
    $d a b c d f k t u v w A $.  $d a u w C $.  $d a c d f u w D $.
    $d a b c d f k t u v w G $.  $d v w H $.  $d a k u w J $.  $d a k u w K $.
    $d a b c d f k t u v w N $.  $d a b c d f k t u v w S $.
    $d a b k t u v w U $.  $d a b c d f k t u v w X $.
    ip1cni.1 $e |- X = ( BaseSet ` U ) $.
    ip1cni.2 $e |- G = ( +v ` U ) $.
    ip1cni.7 $e |- P = ( .i ` U ) $.
    ip1cni.8 $e |- C = ( IndMet ` U ) $.
    ip1cni.d $e |- D = ( abs o. - ) $.
    ip1cni.j $e |- J = ( MetOpen ` C ) $.
    ip1cni.k $e |- K = ( MetOpen ` D ) $.
    ip1cni.f $e |- F = { <. w , v >. | ( w e. X /\ v = ( w P A ) ) } $.
    ip1cni.9 $e |- U e. NrmCVec $.
    ip1cni.a $e |- A e. X $.
    ${
      ip1cnilem.4 $e |- S = ( .s ` U ) $.
      ip1cnilem.6 $e |- N = ( norm ` U ) $.
      ${
        ip1cnilem.12 $e |- H = { <. u , t >. | ( u e. X /\ t =
          ( u G ( ( _i ^ k ) S A ) ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem1 $p |- ( k e. NN -> H e. ( J Cn J ) ) $=
          ( cv cn wcel ci cexp co ccn cn0 nnnn0 ax-icn expcl mpan syl cnv nvscl
          cc mp3an13 va1cn 3syl ) KULZUMUNZUOVKUPUQZVGUNZVMEIUQZRUNZNOOURUQUNVL
          VKUSUNZVNVKUTUOVGUNVQVNVAUOVKVBVCVDJVEUNVNERUNVPUGUHVMEIJRSUIVFVHCDVO
          FJNMORSTUBUDUKUGVIVJ $.
          $( [13-Aug-2007] $)
      $}

      ${
        ip1cnilem.13 $e |- H = { <. u , t >. | ( u e. X /\ t =
          ( N ` ( u G ( ( _i ^ k ) S A ) ) ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem2 $p |- ( k e. NN -> H e. ( J Cn K ) ) $=
          ( va vb cv cn wcel ci cexp co wceq wa copab ccom ccn crn wss wral cn0
          cc nnnn0 ax-icn expcl mpan cnv nvscl mp3an13 3syl nvgcl mp3an1 sylan2
          ancoms ralrimiva eqid oprex rnssopab sylib cfv fvex fveq2 cr wf ax-mp
          wfn nvf ffn dffn5 fopabco syl ip1cnilem1 nmcnc cme w3a imsmet 3pm3.2i
          mpbi cnmet metcnco sylancl eqeltrrd ) KUNZUOUPZQCUNZRUPZDUNXLUQXJURUS
          ZEIUSZMUSZUTVACDVBZVCZNOPVDUSZXKXQVERVFZXRNUTXKXPRUPZCRVGXTXKYACRXMXK
          YAXKXMXORUPZYAXKXJVHUPZXNVIUPZYBXJVJUQVIUPYCYDVKUQXJVLVMJVNUPZYDERUPY
          BUGUHXNEIJRSUIVOVPVQYEXMYBYAUGXLXOJMRSTVRVSVTWAWBCDRRXPXQXQWCZXLXOMWD
          ZWEWFCDULUMDRRXPULUNZQWGZXPQWGXQQNYGYHQWHXPQWHYHXPQWIYFQRWMZQYHRUPUMU
          NYIUTVAULUMVBUTRWJQWKZYJYEYKUGJQRSUJWNWLRWJQWOWLULUMRQWPXEUKWQWRXKXQO
          OVDUSUPZQXSUPZXRXSUPZABCDEFGHIJKLMXQOPQRSTUAUBUCUDUEUFUGUHUIUJYFWSYEY
          MUGFGJOPQUJUBUCUDUEWTWLFXAUPZYOGXAUPZXBYLYMVAYNYOYOYPYEYOUGFJUBXCWLZY
          QGUCXFXDFFGXQQOOPUDUDUEXGVMXHXI $.
          $( [21-Aug-2007] $)
      $}

      ${
        ip1cnilem.14 $e |- H = { <. u , t >. | ( u e. X /\ t =
          ( ( N ` ( u G ( ( _i ^ k ) S A ) ) ) ^ 2 ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem3 $p |- ( k e. NN -> H e. ( J Cn K ) ) $=
          ( va vb cv cn wcel cc c2 cexp co wceq copab cfv ccom ccn crn wss wral
          wa ci cn0 nnnn0 ax-icn expcl mpan cnv nvscl mp3an13 3syl nvgcl mp3an1
          sylan2 ancoms nvcl recnd syl ralrimiva eqid fvex rnssopab sylib oprex
          cr opreq1 fopabco ip1cnilem2 cmpt df-mpt eqcomi sqcn cme imsmet ax-mp
          w3a cnmet 3pm3.2i metcnco sylancl eqeltrrd ) KUNZUOUPZULUNZUQUPUMUNXL
          URUSUTZVAVIULUMVBZCUNZRUPZDUNXOVJXJUSUTZEIUTZMUTZQVCZVAVICDVBZVDZNOPV
          EUTZXKYAVFUQVGZYBNVAXKXTUQUPZCRVHYDXKYECRXKXPVIXSRUPZYEXPXKYFXKXPXRRU
          PZYFXKXJVKUPZXQUQUPZYGXJVLVJUQUPYHYIVMVJXJVNVOJVPUPZYIERUPYGUGUHXQEIJ
          RSUIVQVRVSYJXPYGYFUGXOXRJMRSTVTWAWBWCYFXTYJYFXTWMUPUGXSJQRSUJWDVOWEWF
          WGCDRUQXTYAYAWHZXSQWIZWJWKCDULUMDRUQXTXMXTURUSUTYAXNNYLXLURUSWLXTURUS
          WLXLXTURUSWNYKXNWHUKWOWFXKYAYCUPZXNPPVEUTUPZYBYCUPZABCDEFGHIJKLMYAOPQ
          RSTUAUBUCUDUEUFUGUHUIUJYKWPULGXNPUCUEULUQXMWQXNULUMUQXMWRWSWTFXAUPZGX
          AUPZYQXDYMYNVIYOYPYQYQYJYPUGFJUBXBXCGUCXEZYRXFFGGYAXNOPPUDUEUEXGVOXHX
          I $.
          $( [10-Jan-2014] $) $( [21-Aug-2007] $)
      $}

      ${
        ip1cnilem.15 $e |- H = { <. u , t >. |
              ( u e. X /\ t = ( ( ( _i ^ k ) x. ( 1 / 4 ) )
              x. ( ( N ` ( u G ( ( _i ^ k ) S A ) ) ) ^ 2 ) ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem4 $p |- ( k e. NN -> H e. ( J Cn K ) ) $=
          ( va vb vc vd vf cv cn wcel ci cexp co c1 c4 cdiv cmul cc cfv c2 wceq
          copab ccn cn0 nnnn0 ax-icn expcl mpan 4re recni gt0ne0ii reccli mulcl
          wa 4pos mpan2 3syl eqid ip1cnilem3 cxp c1st c2nd cpr cr clt csup copn
          copab2 imsbai cnmetba cnv cme imsmet ax-mp cnmet mulcn opreq1 opreq1d
          fveq2d fvopab4 opreq2d eqeq2d pm5.32i opabbii eqtr4i opr2cn syl11anc
          oprex ) KUQZURUSZUTXRVAVBZVCVDVEVBZVFVBZVGUSZULUQZRUSUMUQYDXTEIVBZMVB
          ZQVHZVIVAVBZVJWCULUMVKZOPVLVBZUSNYJUSXSXRVMUSZXTVGUSZYCXRVNUTVGUSYKYL
          VOUTXRVPVQYLYAVGUSYCVDVDVRVSVDVRWDVTWAXTYAWBWEWFABULUMEFGHIJKLMYIOPQR
          STUAUBUCUDUEUFUGUHUIUJYIWGZWHUNUOUPCDFGGUNUQZVGVGWIZUSUOUQZYOUSWCUPUQ
          YNWJVHYPWJVHGVBYNWKVHYPWKVHGVBWLWMWNWOVJWCUNUOUPWQZYBPYINGOPPYQWPVHZV
          FRVGVGFJRSUBUGWRGUCWSZYSJWTUSFXAUSUGFJUBXBXCGUCXDZYTYTUDUEUEYRWGZUEYQ
          WGZUNUOUPGYQPYRUCUUBUEUUAXENCUQZRUSZDUQZYBUUCYEMVBZQVHZVIVAVBZVFVBZVJ
          ZWCZCDVKUUDUUEYBUUCYIVHZVFVBZVJZWCZCDVKUKUUOUUKCDUUDUUNUUJUUDUUMUUIUU
          EUUDUULUUHYBVFULUMUUCYHUUHRYIYDUUCVJZYGUUGVIVAUUPYFUUFQYDUUCYEMXFXHXG
          YMUUGVIVAXQXIXJXKXLXMXNXOXP $.
          $( [21-Aug-2007] $)
      $}


      ${
        ip1cnilem.16 $e |- H = { <. u , t >. |
               ( u e. X /\ t = ( ( ( _i ^ k ) x. ( ( N ` ( u G (
                 ( _i ^ k ) S A ) ) ) ^ 2 ) ) x. ( 1 / 4 ) ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem5 $p |- ( k e. NN -> H e. ( J Cn K ) ) $=
          ( cv cn wcel ci cexp co c1 c4 cdiv cmul cfv c2 wa copab ccn cc ax-icn
          wceq cn0 nnnn0 expcl sylancr adantr cnv mpan nvscl mp3an13 3syl nvgcl
          cr mp3an1 sylan2 ancoms nvcl recnd sqcl syl 4re recni gt0ne0ii reccli
          4pos mul32 mp3an3 syl11anc eqeq2d pm5.32da opabbidv syl5eq ip1cnilem4
          eqid eqeltrd ) KULZUMUNZNCULZRUNZDULZUOXDUPUQZURUSUTUQZVAUQXFXIEIUQZM
          UQZQVBZVCUPUQZVAUQZVIZVDZCDVEZOPVFUQXENXGXHXIXNVAUQXJVAUQZVIZVDZCDVEX
          RUKXEYAXQCDXEXGXTXPXEXGVDZXSXOXHYBXIVGUNZXNVGUNZXSXOVIZXEYCXGXEUOVGUN
          ZXDVJUNZYCVHXDVKZUOXDVLZVMVNYBXMVGUNYDYBXMYBJVOUNZXLRUNZXMWAUNUGXGXEY
          KXEXGXKRUNZYKXEYGYCYLYHYFYGYCVHYIVPYJYCERUNYLUGUHXIEIJRSUIVQVRVSYJXGY
          LYKUGXFXKJMRSTVTWBWCWDXLJQRSUJWEVMWFXMWGWHYCYDXJVGUNYEUSUSWIWJUSWIWMW
          KWLXIXNXJWNWOWPWQWRWSWTABCDEFGHIJKLMXROPQRSTUAUBUCUDUEUFUGUHUIUJXRXBX
          AXC $.
          $( [21-Aug-2007] $)

        $( Lemma for ~ ip1cni . $)
        ip1cnilem6 $p |- F e. ( J Cn K ) $=
          ( c4 cn wcel ccn co 4nn cnv imsmet ax-mp imsbai ip1cnilem5 cv wceq wa
          cme copab c1 cfz cfv csu ci cexp c2 cmul cdiv cc cuz wral elnnuz mpbi
          elfznn cn0 ax-icn nnnn0 expcl sylancr adantl nvscl mp3an13 syl mp3an1
          cr nvgcl sylan2 nvcl recnd sqcl syl11anc ralrimiva fsumcl cc0 wne 4re
          mulcl recni 4pos gt0ne0ii divrec mp3an23 ipval opreq1 opreq1d opreq2d
          fveq2d oprex fvopab4 sumeq2sdv reccli fsummulc2 mp3an12 eqtr4d eqeq2d
          3eqtr4rd pm5.32i opabbii eqtr4i fsumcn ) ULUMUNZLOPUOUPUNUQABFGKNLOPU
          LRJURUNZFVFUNUGFJUBUSUTFJRSUBUGVAUCUDUEABCDEFGHIJKLMNOPQRSTUAUBUCUDUE
          UFUGUHUIUJUKVBLAVCZRUNZBVCZYKEHUPZVDZVEZABVGYLYMVHULVIUPZYKNVJZKVKZVD
          ZVEZABVGUFUUAYPABYLYTYOYLYSYNYMYLYQVLKVCZVMUPZYKUUCEIUPZMUPZQVJZVNVMU
          PZVOUPZKVKZULVPUPZUUIVHULVPUPZVOUPZYNYSYLUUIVQUNZUUJUULVDZYLULVHVRVJU
          NZUUHVQUNZKYQVSZUUMYIUUOUQULVTWAZYLUUPKYQUUBYQUNYLUUBUMUNZUUPUUBULWBY
          LUUSVEZUUCVQUNZUUGVQUNZUUPUUSUVAYLUUSVLVQUNUUBWCUNUVAWDUUBWEVLUUBWFWG
          ZWHUUTUUFVQUNUVBUUTUUFUUTYJUUERUNZUUFWMUNUGUUSYLUUDRUNZUVDUUSUVAUVEUV
          CYJUVAERUNZUVEUGUHUUCEIJRSUIWIWJWKYJYLUVEUVDUGYKUUDJMRSTWNWLWOUUEJQRS
          UJWPWGWQUUFWRWKUUCUUGXEWSWOWTZUUHKVHULXAWGUUMULVQUNULXBXCUUNULXDXFZUL
          XDXGXHZUUIULXIXJWKYJYLUVFYNUUJVDUGUHYKEHIJKMQRSTUIUJUAXKWJYLYSYQUUHUU
          KVOUPZKVKZUULYLYQYRUVJKCDYKUUCCVCZUUDMUPZQVJZVNVMUPZVOUPZUUKVOUPUVJRN
          UVLYKVDZUVPUUHUUKVOUVQUVOUUGUUCVOUVQUVNUUFVNVMUVQUVMUUEQUVLYKUUDMXLXO
          XMXNXMUKUUHUUKVOXPXQXRYLUUQUULUVKVDZUVGUUOUUKVQUNUUQUVRUURULUVHUVIXSU
          UHUUKKVHULXTYAWKYBYDYCYEYFYGYHUT $.
          $( [21-Aug-2007] $)
      $}
    $}

    $( Inner product is continuous in its first operand. $)
    ip1cni $p |- F e. ( J Cn K ) $=
      ( vu vt vk cns cfv cv wcel ci cexp co cnm c2 cmul c1 c4 cdiv wceq wa eqid
      copab ip1cnilem6 ) ABUCUDCDEFGUFUGZGUEHIUCUHZLUIUDUHUJUEUHUKULZVEVFCVDULI
      ULGUMUGZUGUNUKULUOULUPUQURULUOULUSUTUCUDVBZJKVGLMNOPQRSTUAUBVDVAVGVAVHVAV
      C $.
      $( [21-Aug-2007] $)
  $}

