$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                            Inner product
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c .i $.

  $( Extend class notation with the class inner product functions. $)
  cip $a class .i $.

  ${
    $d k n p w x y z $.
    $( Define a function that maps a complex normed vector space to its inner
       product operation in case its norm satisfies the parallelogram identity
       (otherwise the operation is still defined, but not meaningful).  Based
       on Exercise 4(a) of [ReedSimon] p. 63 and Theorem 6.44 of [Ponnusamy]
       p. 361.  Vector addition is ` ( 1st `` w ) ` , the scalar product is
       ` ( 2nd `` w ) ` , and the norm is ` n ` . $)
    df-ip $a |- .i = { <. <. w , n >. , p >. | ( <. w , n >. e. NrmCVec /\
        p = { <. <. x , y >. , z >. | ( ( x e. dom n /\ y e. dom n ) /\
        z = ( sum_ k e. ( 1 ... 4 ) ( ( _i ^ k ) x. ( ( n ` ( x ( 1st ` w )
        ( ( _i ^ k ) ( 2nd ` w ) y ) ) ) ^ 2 ) ) / 4 ) ) } ) } $.

$(
    df-ip $a |- .i = { <. x , y >. | ( x e. NrmCVec /\ E. g E. s E. n
            ( x = <. <. g , s >. , n >. /\ y = { <. <. a , b >. , c >. |
            ( ( a e. dom n /\ b e. dom n ) /\ c = ( sum_ k e. ( 1 ... 4 )
             ( ( _i ^ k ) x. ( ( n ` ( a g ( ( _i ^ k ) s b ) ) ) ^ 2 ) )
              / 4 ) ) } ) ) } $.

    df-ip $a |- .i = { <. x , y >. | ( x e. NrmCVec /\ E. g E. s E. n
            ( x = <. <. g , s >. , n >. /\ y = { <. <. a , b >. , c >. |
            ( ( a e. dom n /\ b e. dom n ) /\ c =
                ( ( ( ( ( n ` ( a g b ) ) ^ 2 ) +
                ( ( n ` ( a g ( -u 1 s b ) ) ) ^ 2 ) ) +
                ( _i x. ( ( ( n ` ( a g ( _i s b ) ) ) ^ 2 ) +
                ( ( n ` ( a g ( -u _i s b ) ) ) ^ 2 ) ) ) ) / 4 ) ) } ) ) } $.
$)
  $}

  ${
    $d k A $.  $d k B $.  $d k G $.  $d k N $.  $d k S $.
    ipval2lem.1 $e |- M e. _V $.
    $( Lemma for ~ ipval3 . $)
    ipval2lem1 $p |- [_ M / k ]_ ( ( _i ^ k ) x.
                           ( ( N ` ( A G ( ( _i ^ k ) S B ) ) ) ^ 2 ) ) =
              ( ( _i ^ M ) x. ( ( N ` ( A G ( ( _i ^ M ) S B ) ) ) ^ 2 ) ) $=
      ( ci cv cexp co cfv c2 cmul csb cvv wcel wceq csbopr12g ax-mp csbopr2g
      csbvarg opreq2i eqtri csbopr1g csbfv2g opreq1i fveq2i opreq12i ) DFIDJZKL
      ZAULBCLZELZGMZNKLZOLPZDFULPZDFUPPZOLZIFKLZAVABCLZELZGMZNKLZOLFQRZUQUTSHDF
      ULUPQOTUAURVAUSVEOURIDFUKPZKLZVAVFURVHSHDFIUKQKUBUAVGFIKVFVGFSHDFQUCUAUDU
      EZUSDFUOPZNKLZVEVFUSVKSHDFUONQKUFUAVJVDNKVJDFUNPZGMZVDVFVJVMSHDFUNQGUGUAV
      LVCGVLADFUMPZELZVCVFVLVOSHDFAUMQEUBUAVNVBAEVNURBCLZVBVFVNVPSHDFULBQCUFUAU
      RVABCVIUHUEUDUEUIUEUHUEUJUE $.
      $( [31-Jan-2007] $)
  $}

  ${
    $d k n p w x y z G $.  $d k n p w x y z N $.  $d k n p w x y z S $.
    $d k n p w x y z U $.  $d k x y z A $.  $d k x y z B $.  $d x y z X $.
    ipfval.1 $e |- X = ( BaseSet ` U ) $.
    ipfval.2 $e |- G = ( +v ` U ) $.
    ipfval.4 $e |- S = ( .s ` U ) $.
    ipfval.6 $e |- N = ( norm ` U ) $.
    ipfval.7 $e |- P = ( .i ` U ) $.
    $( The inner product function on a normed complex vector space.  The
       definition is meaningful for vector spaces that are also inner product
       spaces, i.e. satisfy the parallelogram law. $)
    ipfval $p |- ( U e. NrmCVec -> P = { <. <. x , y >. , z >. |
        ( ( x e. X /\ y e. X ) /\
       z = ( sum_ k e. ( 1 ... 4 ) ( ( _i ^ k ) x. ( ( N ` ( x G
       ( ( _i ^ k ) S y ) ) ) ^ 2 ) ) / 4 ) ) } ) $=
      ( vw vn vp cnv wcel c1st cfv c2nd cop cip co cv wa c1 c4 cfz ci cexp c2
      cmul csu cdiv wceq copab2 wrel nvrel 1st2nd mpan fveq2d syl5eq df-opr
      a1i cdm eleq1d ibi cvv fvex w3a cnm eqeltri dmex eqid oprabex2 visset
      opth wb id nmfval syl6eqr dmeqd eleq2d anbi12d adantl fveq1d fveq2
      vafval opreqd smfval opreq2d eqtrd sylan9eqr opreq1d sumeq2sdv eqeq2d
      oprabbidv sylbi df-ip oprabval6g mpan2 mp3an12 syl cr wf nvf fdm anbi1d
      3eqtr2d ) FSTZDFUAUBZFUCUBZUDZUEUBZXNXOUEUFZAUGZJTZBUGZJTZUHZCUGZUIUJUKUF
      ZULGUGUMUFZXSYFYAEUFZHUFZIUBZUNUMUFZUOUFZGUPZUJUQUFZURZUHZABCUSZXMDFUEUBX
      QOXMFXPUESUTXMFXPURVAFSVBVCZVDVEXRXQURXMXNXOUEVFVGXMXRXSIVHZTZYAYRTZUHZYN
      UHZABCUSZYPXMXPSTZXRUUCURZXMUUDXMFXPSYQVIVJXNVKTZXOVKTZUUDUUEFUAVLFUCVLZU
      UFUUGUUDVMUUCVKTUUEABCYRYRYMUUCIIFVNUBVKNFVNVLVOVPZUUIUUCVQVRPQRXNXOSXSQU
      GZVHZTZYAUUKTZUHZYDYEYFXSYFYAPUGZUCUBZUFZUUOUAUBZUFZUUJUBZUNUMUFZUOUFZGUP
      ZUJUQUFZURZUHZABCUSZUUCUEVKVKVKUUOUUJUDXPURUUOXNURZUUJXOURZUHZUVGUUCURUUO
      UUJXNXOPVSQVSUUHVTUVJUVFUUBABCUVJUUNUUAUVEYNUVIUUNUUAWAUVHUVIUULYSUUMYTUV
      IUUKYRXSUVIUUJIUVIUUJXOIUVIWBFINWCWDZWEZWFUVIUUKYRYAUVLWFWGWHUVJUVDYMYDUV
      JUVCYLUJUQUVJYEUVBYKGUVJUVAYJYFUOUVJUUTYIUNUMUVIUVHUUTUUSIUBYIUVIUUSUUJIU
      VKWIUVHUUSYHIUVHUUSXSUUQHUFYHUVHUURHXSUUQUVHUURXNUAUBHUUOXNUAWJFHLWKWDWLU
      VHUUQYGXSHUVHUUPEYFYAUVHUUPXNUCUBEUUOXNUCWJEFMWMWDWLWNWOVDWPWQWNWRWQWSWGW
      TXAABCPGQRXBXCXDXEXFXMUUBYOABCXMUUAYCYNXMYSXTYTYBXMYRJXSXMJXGIXHYRJURFIJK
      NXIJXGIXJXFZWFXMYRJYAUVMWFWGXKWTWOXL $.
      $( [10-Apr-2007] $)

    $( Value of the inner product.  The definition is meaningful for normed
       complex vector spaces that are also inner product spaces, i.e. satisfy
       the parallelogram law, although for convenience we define it for any
       normed complex vector space.  The vector (group) addition operation is
       ` G ` , the scalar product is ` S ` , the norm is ` N ` , and the set of
       vectors is ` X ` .  Equation 6.45 of [Ponnusamy] p. 361. $)
    ipval $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B ) =
                ( sum_ k e. ( 1 ... 4 ) ( ( _i ^ k ) x. ( ( N ` ( A G
                    ( ( _i ^ k ) S B ) ) ) ^ 2 ) ) / 4 ) ) $=
      ( vx vy vz cnv wcel w3a co cv wa c1 c4 cfz ci cexp cfv c2 cmul csu cdiv
      wceq copab2 ipfval opreqd 3ad2ant1 oprex opreq1 fveq2d opreq1d opreq2d
      sumeq2sdv opreq2 eqid oprabval2 3adant1 eqtrd ) ERSZAISZBISZTABCUAZABOUBZ
      ISPUBZISUCQUBUDUEUFUAZUGFUBUHUAZVNVQVODUAZGUAZHUIZUJUHUAZUKUAZFULZUEUMUAZ
      UNUCOPQUOZUAZVPVQAVQBDUAZGUAZHUIZUJUHUAZUKUAZFULZUEUMUAZVJVKVMWFUNVLVJCWE
      ABOPQCDEFGHIJKLMNUPUQURVKVLWFWMUNVJOPQABIIWDWMWEVPVQAVRGUAZHUIZUJUHUAZUKU
      AZFULZUEUMUAWLUEUMUSVNAUNZWCWRUEUMWSVPWBWQFWSWAWPVQUKWSVTWOUJUHWSVSWNHVNA
      VRGUTVAVBVCVDVBVOBUNZWRWLUEUMWTVPWQWKFWTWPWJVQUKWTWOWIUJUHWTWNWHHWTVRWGAG
      VOBVQDVEVCVAVBVCVDVBWEVFVGVHVI $.
      $( [29-Sep-2007] $) $( [31-Jan-2007] $)

    $( Lemma for ~ ipval3 . $)
    ipval2lem2 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
               ( ( N ` ( A G ( C S B ) ) ) ^ 2 ) e. RR ) $=
      ( cnv wcel w3a cc wa co cfv cr c2 cexp simp1 adantr simp2 nvscl 3com23
      3expa 3adantl2 nvgcl syl111anc nvcl syl11anc resqcl syl ) FOPZAIPZBIPZQZC
      RPZSZACBETZGTZHUAZUBPZVFUCUDTUBPVCURVEIPZVGVAURVBURUSUTUEUFZVCURUSVDIPZVH
      VIVAUSVBURUSUTUGUFURUTVBVJUSURUTVBVJURVBUTVJCBEFIJLUHUIUJUKAVDFGIJKULUMVE
      FHIJMUNUOVFUPUQ $.
      $( [1-Oct-2007] $) $( [1-Feb-2007] $)

    $( Lemma for ~ ipval3 . $)
    ipval2lem3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( ( N ` ( A G B ) ) ^ 2 ) e. RR ) $=
      ( cnv wcel w3a c1 co cfv c2 cexp cr wceq wa nvsid opreq2d fveq2d opreq1d
      3adant2 cc ax1cn ipval2lem2 mpan2 eqeltrrd ) ENOZAHOZBHOZPZAQBDRZFRZGSZTU
      ARZABFRZGSZTUARZUBUOUQVBVEUCUPUOUQUDZVAVDTUAVFUTVCGVFUSBAFBDEHIKUEUFUGUHU
      IURQUJOVBUBOUKABQCDEFGHIJKLMULUMUN $.
      $( [1-Oct-2007] $) $( [1-Feb-2007] $)

    $( Lemma for ~ ipval3 . $)
    ipval2lem4 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
               ( ( N ` ( A G ( C S B ) ) ) ^ 2 ) e. CC ) $=
      ( cnv wcel w3a cc wa co cfv c2 cexp ipval2lem2 recnd ) FOPAIPBIPQCRPSACBE
      TGTHUAUBUCTABCDEFGHIJKLMNUDUE $.
      $( [1-Oct-2007] $) $( [1-Feb-2007] $)

    $( Expansion of the inner product value ~ ipval . _Warning:  The HTML proof
       page is 0.5MB in size_. $)
    ipval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B )
               = ( ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) +
               ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u _i S B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
      ( vk cnv wcel w3a co c1 c4 cfz ci cv cexp cfv c2 cmul csu cdiv cneg cmin
      caddc ipval cc wceq axicn ipval2lem4 mpan2 mulcl mpan syl ax1cn negcli
      subcl syl11anc negsub mulm1 opreq2d eqtrd mulneg1 opreq12d subdi mp3an1
      opreq1d sub23 syl111anc 3eqtr4d wa i4 a1i opreq1i nvsid syl5eq fveq2d
      3adant2 ipval2lem3 recnd mulid2 c3 csb df-4 3re recni addcomi eqtri
      opreq2i sumeq1i cz 1z oprex fsum4 ax-mp elisseti ipval2lem1 exp1 fveq2i
      opreq12i 2cn df-2 csbeq1 i2 3eqtr3i cr df-3 i3 4re eqtr3i 3eqtri addcom
      subadd23 eqtr4d ) EOPZAHPZBHPZQZABCRSTUARZUBNUCUDRZAYGBDRFRGUEUFUDRZUGRZN
      UHZTUIRABFRZGUEZUFUDRZASUJZBDRZFRZGUEZUFUDRZUKRZUBAUBBDRZFRZGUEZUFUDRZAUB
      UJZBDRZFRZGUEZUFUDRZUKRZUGRZULRZTUIRABCDENFGHIJKLMUMYEYJUUKTUIYEUBUUCUGRZ
      YNYRUGRZULRZUUDUUHUGRZULRZUBTUDRZAUUQBDRZFRZGUEZUFUDRZUGRZULRZUUJYRUKRZYM
      ULRZYJUUKYEUUPUVDUVBYMULYEUULYRUKRZUBUUHUGRZUJZULRZUVFUVGUKRZUUPUVDYEUVFU
      NPZUVGUNPZUVIUVJUOYEUULUNPZYRUNPZUVKYEUUCUNPZUVMYEUBUNPZUVOUPABUBCDEFGHIJ
      KLMUQURZUVPUVOUVMUPUBUUCUSUTVAZYEYNUNPUVNSVBVCABYNCDEFGHIJKLMUQURZUULYRVD
      VEYEUUHUNPZUVLYEUUDUNPUVTUBUPVCABUUDCDEFGHIJKLMUQURZUVPUVTUVLUPUBUUHUSUTV
      AZUVFUVGVFVEYEUUNUVFUUOUVHULYEUUNUULYRUJZULRZUVFYEUUMUWCUULULYEUVNUUMUWCU
      OUVSYRVGVAVHYEUVMUVNUWDUVFUOUVRUVSUULYRVFVEVIYEUVTUUOUVHUOZUWAUVPUVTUWEUP
      UBUUHVJUTVAVKYEUVDUULUVGUKRZYRUKRZUVJYEUUJUWFYRUKYEUVOUVTUUJUWFUOZUVQUWAU
      VPUVOUVTUWHUPUBUUCUUHVLVMVEVNYEUVMUVLUVNUWGUVJUOUVRUWBUVSUULUVGYRVOVPVIVQ
      YEUVBSYMUGRZYMYBYDUVBUWIUOYCYBYDVRZUUQSUVAYMUGUUQSUOUWJVSVTUWJUUTYLUFUDUW
      JUUSYKGUWJUURBAFUWJUURSBDRBUUQSBDVSWABDEHIKWBWCVHWDVNVKWEYEYMUNPZUWIYMUOY
      EYMABCDEFGHIJKLMWFWGZYMWHVAVIVKYJUVCUOYEYJSSWIULRZUARZYINUHZNSYIWJZNSSULR
      ZYIWJZULRZNSUFULRZYIWJZULRZNUWMYIWJZULRZUVCYFUWNYINTUWMSUATWISULRUWMWKWIS
      WIWLWMVBWNWOZWPWQSWRPUWOUXDUOWSYINSYGYHUGWTXAXBUXBUUPUXCUVBULUWSUUNUXAUUO
      ULUWPUULUWRUUMULUWPUBSUDRZAUXFBDRZFRZGUEZUFUDRZUGRUULABDNFSGSWRWSXCXDUXFU
      BUXJUUCUGUVPUXFUBUOUPUBXEXBZUXIUUBUFUDUXHUUAGUXGYTAFUXFUBBDUXKWAWPXFWAXGW
      ONUFYIWJZUBUFUDRZAUXMBDRZFRZGUEZUFUDRZUGRUWRUUMABDNFUFGUFUNXHXCXDUFUWQUOU
      XLUWRUOXINUFUWQYIXJXBUXMYNUXQYRUGXKUXPYQUFUDUXOYPGUXNYOAFUXMYNBDXKWAWPXFW
      AXGXLXGNWIYIWJZUBWIUDRZAUXSBDRZFRZGUEZUFUDRZUGRUXAUUOABDNFWIGWIXMWLXCXDWI
      UWTUOUXRUXAUOWIUFSULRUWTXNUFSXHVBWNWONWIUWTYIXJXBUXSUUDUYCUUHUGXOUYBUUGUF
      UDUYAUUFGUXTUUEAFUXSUUDBDXOWAWPXFWAXGXLXGNTYIWJZUXCUVBTUWMUOUYDUXCUOUXENT
      UWMYIXJXBABDNFTGTXMXPXCXDXQXGXRVTYEUUKUUJYSULRZUVEYEYSUNPZUUJUNPZUUKUYEUO
      YEUWKUVNUYFUWLUVSYMYRVDVEYEUUIUNPZUYGYEUVOUVTUYHUVQUWAUUCUUHVDVEUVPUYHUYG
      UPUBUUIUSUTVAZYSUUJXSVEYEUYGUVNUWKUVEUYEUOUYIUVSUWLUUJYRYMXTVPYAVQVNVI $.
      $( [1-Oct-2007] $) $( [31-Jan-2007] $)

    $( Four times the inner product value ~ ipval3 , useful for simplifying
       certain proofs. $)
    4ipval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
        ( 4 x. ( A P B ) ) = ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) +
               ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u _i S B ) ) ) ^ 2 ) ) ) ) ) $=
      ( cnv wcel w3a c4 co cmul cfv c2 cexp c1 cneg cmin ci caddc cdiv ipval2
      opreq2d cc wceq cr simp1 nvgcl nvcl syl11anc recnd sqcl syl ax1cn negcli
      nvscl mp3an2 3adant2 syld3an3 subcl axicn mulcl mpan addcl cc0 wne 4re
      recni 4pos gt0ne0ii divcan2 mp3an23 eqtrd ) ENOZAHOZBHOZPZQABCRZSRQABFRZG
      TZUAUBRZAUCUDZBDRZFRZGTZUAUBRZUERZUFAUFBDRZFRZGTZUAUBRZAUFUDZBDRZFRZGTZUA
      UBRZUERZSRZUGRZQUHRZSRZXFWDWEXGQSABCDEFGHIJKLMUIUJWDXFUKOZXHXFULZWDWNUKOZ
      XEUKOZXIWDWHUKOZWMUKOZXKWDWGUKOXMWDWGWDWAWFHOWGUMOWAWBWCUNZABEFHIJUOWFEGH
      ILUPUQURWGUSUTWDWLUKOXNWDWLWDWAWKHOZWLUMOXOWAWBWCWJHOZXPWAWCXQWBWAWIUKOWC
      XQUCVAVBWIBDEHIKVCVDVEAWJEFHIJUOVFWKEGHILUPUQURWLUSUTWHWMVGUQWDXDUKOZXLWD
      WRUKOZXCUKOZXRWDWQUKOXSWDWQWDWAWPHOZWQUMOXOWAWBWCWOHOZYAWAWCYBWBWAUFUKOZW
      CYBVHUFBDEHIKVCVDVEAWOEFHIJUOVFWPEGHILUPUQURWQUSUTWDXBUKOXTWDXBWDWAXAHOZX
      BUMOXOWAWBWCWTHOZYDWAWCYEWBWAWSUKOWCYEUFVHVBWSBDEHIKVCVDVEAWTEFHIJUOVFXAE
      GHILUPUQURXBUSUTWRXCVGUQYCXRXLVHUFXDVIVJUTWNXEVKUQXIQUKOQVLVMXJQVNVOQVNVP
      VQXFQVRVSUTVT $.
      $( [10-Apr-2007] $)

    ${
      ipval3.3 $e |- M = ( -v ` U ) $.
      $( Expansion of the inner product value ~ ipval . $)
      ipval3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B )
               = ( ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A M B ) ) ^ 2 ) ) +
               ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A M ( _i S B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
        ( cnv wcel w3a co cfv c2 cexp c1 cneg cmin ci cmul caddc c4 cdiv
        ipval2 nvmval fveq2d opreq1d opreq2d wceq cc axicn nvscl mp3an2
        3adant2 syld3an3 wa mulm1i opreq1i ax1cn negcli nvsass mp3anr1 mpanr1
        syl5reqr eqtrd opreq12d eqtr4d ) EPQZAIQZBIQZRZABCSABFSHTUAUBSZAUCUDZBD
        SFSZHTZUAUBSZUESZUFAUFBDSZFSHTUAUBSZAUFUDZBDSZFSZHTZUAUBSZUESZUGSZUHSZU
        IUJSVSABGSZHTZUAUBSZUESZUFWFAWEGSZHTZUAUBSZUESZUGSZUHSZUIUJSABCDEFHIJKL
        MNUKVRXDWNUIUJVRWRWDXCWMUHVRWQWCVSUEVRWPWBUAUBVRWOWAHABDEFGIJKLOULUMUNU
        OVRXBWLUFUGVRXAWKWFUEVRWTWJUAUBVRWSWIHVRWSAVTWEDSZFSZWIVOVPVQWEIQZWSXFU
        PVOVQXGVPVOUFUQQZVQXGURUFBDEIJLUSUTVAAWEDEFGIJKLOULVBVRXEWHAFVOVQXEWHUP
        VPVOVQVCWHVTUFUGSZBDSZXEXIWGBDUFURVDVEVOXHVQXJXEUPZURVOVTUQQXHVQXKUCVFV
        GVTUFBDEIJLVHVIVJVKVAUOVLUMUNUOUOVMUNVN $.
        $( [17-Nov-2007] $)

      $( Lemma for ~ ipval3 . $)
      ipval2lem5 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
                 ( ( N ` ( A M ( C S B ) ) ) ^ 2 ) e. RR ) $=
        ( cnv wcel w3a cc wa co cfv cr c2 cexp simp1 adantr simp2 nvscl 3com23
        3expa 3adantl2 nvmcl syl111anc nvcl syl11anc resqcl syl ) FQRZAJRZBJRZS
        ZCTRZUAZACBEUBZHUBZIUCZUDRZVHUEUFUBUDRVEUTVGJRZVIVCUTVDUTVAVBUGUHZVEUTV
        AVFJRZVJVKVCVAVDUTVAVBUIUHUTVBVDVLVAUTVBVDVLUTVDVBVLCBEFJKMUJUKULUMAVFF
        HJKPUNUOVGFIJKNUPUQVHURUS $.
        $( [1-Feb-2008] $)

      $( Lemma for ~ ipval3 . $)
      ipval2lem6 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                 ( ( N ` ( A M B ) ) ^ 2 ) e. RR ) $=
        ( cnv wcel w3a c1 co cfv c2 cexp cr wceq wa nvsid opreq2d fveq2d
        opreq1d 3adant2 cc ax1cn ipval2lem5 mpan2 eqeltrrd ) EPQZAIQZBIQZRZASBD
        TZGTZHUAZUBUCTZABGTZHUAZUBUCTZUDUQUSVDVGUEURUQUSUFZVCVFUBUCVHVBVEHVHVAB
        AGBDEIJLUGUHUIUJUKUTSULQVDUDQUMABSCDEFGHIJKLMNOUNUOUP $.
        $( [1-Feb-2008] $)

      $( Four times the inner product value ~ ipval3 , useful for simplifying
         certain proofs. $)
      4ipval3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
          ( 4 x. ( A P B ) ) = ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
                 ( ( N ` ( A M B ) ) ^ 2 ) ) +
                 ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
                 ( ( N ` ( A M ( _i S B ) ) ) ^ 2 ) ) ) ) ) $=
        ( cnv wcel w3a c4 co cmul cfv c2 cexp cmin ci caddc cdiv ipval3
        opreq2d cc wceq ipval2lem3 recnd ipval2lem6 subcl syl11anc cr axicn
        ipval2lem2 mpan2 ipval2lem5 mulcl mpan syl addcl cc0 wne 4re recni
        4pos gt0ne0ii divcan2 mp3an23 eqtrd ) EPQAIQBIQRZSABCTZUATSABFTHUBUCUDT
        ZABGTHUBUCUDTZUETZUFAUFBDTZFTHUBUCUDTZAWAGTHUBUCUDTZUETZUATZUGTZSUHTZUA
        TZWFVPVQWGSUAABCDEFGHIJKLMNOUIUJVPWFUKQZWHWFULZVPVTUKQZWEUKQZWIVPVRUKQV
        SUKQWKVPVRABCDEFHIJKLMNUMUNVPVSABCDEFGHIJKLMNOUOUNVRVSUPUQVPWDUKQZWLVPW
        BUKQWCUKQWMVPWBVPUFUKQZWBURQUSABUFCDEFHIJKLMNUTVAUNVPWCVPWNWCURQUSABUFC
        DEFGHIJKLMNOVBVAUNWBWCUPUQWNWMWLUSUFWDVCVDVEVTWEVFUQWISUKQSVGVHWJSVIVJS
        VIVKVLWFSVMVNVEVO $.
        $( [1-Feb-2008] $)
    $}
  $}

  ${
    $d k n p w x y z G $.  $d k n p w x y z N $.  $d k n p w x y z S $.
    $d k n p w x y z U $.  $d k x y z A $.  $d k x y z B $.  $d x y z X $.
    ipid.1 $e |- X = ( BaseSet ` U ) $.
    ipid.6 $e |- N = ( norm ` U ) $.
    ipid.7 $e |- P = ( .i ` U ) $.
    $( The inner product of a vector with itself is the square of the vector's
       norm.  Equation I4 of [Ponnusamy] p. 362. $)
    ipid $p |- ( ( U e. NrmCVec /\ A e. X ) ->
                ( A P A ) = ( ( N ` A ) ^ 2 ) ) $=
      ( cnv wcel wa co cpv cfv c2 cexp c1 cneg cns cmin ci cmul caddc c4 cdiv
      wceq eqid ipval2 3anidm23 cc0 c1st cvc nvvc vafval smfval bafval vc2
      sylan fveq2d cr cle wbr 2nn nnrei 0re 2re 2pos ltleii pm3.2i nvsge0
      mp3an2 eqtrd opreq1d cc nvcl recnd cn0 2cn 2nn0 mulexp mp3an13 syl sq2
      opreq1i a1i 3eqtrd cn0v nvrinv nvz0 adantr sq0 syl6eq opreq12d 4re recni
      sqcl mulcl sylancr subid1 cabs csqr 1re renegcli absreim mp2an axicn
      ax1cn mulneg2i mulid1i negeqi eqtri opreq2i fveq2i sqneg ax-mp 3eqtr3i
      3eqtr2i negcli addcli nvs 3eqtr4a nvdir mp3anr1 mpanr1 nvsid 3eqtr3d
      opreq2d w3a ipval2lem4 mpan2 subid mul01i addid1 eqtr2d wne 4pos
      gt0ne0ii divcan3 mp3an23 3eqtr2d ) CIJZAEJZKZAABLZAACMNZLZDNZOPLZAQRZACSN
      ZLUUELZDNZOPLZTLZUAAUAAUUJLZUUELZDNZOPLZAUARZAUUJLZUUELZDNZOPLZTLZUBLZUCL
      ZUDUELZUDADNZOPLZUBLZUDUELZUVIUUAUUBUUDUVGUFAABUUJCUUEDEFUUEUGZUUJUGZGHUH
      UIUUCUVJUVFUDUEUUCUVFUVJUJUCLZUVJUUCUUNUVJUVEUJUCUUCUUNUVJUJTLZUVJUUCUUHU
      VJUUMUJTUUCUUHOUVHUBLZOPLZOOPLZUVIUBLZUVJUUCUUGUVPOPUUCUUGOAUUJLZDNZUVPUU
      CUUFUVTDUUACUKNZULJUUBUUFUVTUFCUWBUWBUGUMAUUJUUEUWBECUUEUVLUNUUJCUVMUOCUU
      EEFUVLUPUQURUSUUAOUTJZUJOVAVBZKUUBUWAUVPUFUWCUWDOVCVDUJOVEVFVGVHVIOAUUJCD
      EFUVMGVJVKVLVMUUCUVHVNJZUVQUVSUFZUUCUVHACDEFGVOVPZOVNJUWEOVQJUWFVRVSOUVHO
      VTWAWBUVSUVJUFUUCUVRUDUVIUBWCWDWEWFUUCUUMUJOPLUJUUCUULUJOPUUCUULCWGNZDNZU
      JUUCUUKUWHDAUUJCUUEEUWHFUVLUVMUWHUGZWHUSUUAUWIUJUFUUBCDUWHUWJGWIWJVLVMWKW
      LWMUUCUVJVNJZUVOUVJUFUUCUDVNJZUVIVNJZUWKUDWNWOZUUCUWEUWMUWGUVHWPWBZUDUVIW
      QWRZUVJWSWBVLUUCUVEUAUJUBLUJUUCUVDUJUAUBUUCUVDUURUURTLZUJUUCUVCUURUURTUUC
      UVBUUQOPUUCQUUSUCLZAUUJLZDNZQUAUCLZAUUJLZDNZUVBUUQUUCUWRWTNZUVHUBLZUXAWTN
      ZUVHUBLZUWTUXCUXDUXFUVHUBUXDQOPLZUXHUCLZXANZQUAQUBLZUCLZWTNZUXFQUAUUIUBLZ
      UCLZWTNZUXHUUIOPLZUCLZXANZUXDUXJQUTJZUUIUTJUXPUXSUFXBQXBXCQUUIXDXEUXOUWRW
      TUXNUUSQUCUXNUXKRUUSUAQXFXGXHUXKUAUAXFXIZXJXKXLXMUXRUXIXAUXQUXHUXHUCQVNJZ
      UXQUXHUFXGQXNXOXLXMXPUXTUXTUXMUXJUFXBXBQQXDXEUXLUXAWTUXKUAQUCUYAXLXMXQWDU
      UAUWRVNJUUBUWTUXEUFQUUSXGUAXFXRZXSUWRAUUJCDEFUVMGXTVKUUAUXAVNJUUBUXCUXGUF
      QUAXGXFXSUXAAUUJCDEFUVMGXTVKYAUUCUWSUVADUUCUWSQAUUJLZUUTUUELZUVAUUAUUSVNJ
      ZUUBUWSUYEUFZUYCUUAUYBUYFUUBUYGXGQUUSAUUJCUUEEFUVLUVMYBYCYDUUCUYDAUUTUUEA
      UUJCEFUVMYEZVMVLUSUUCUXBUUPDUUCUXBUYDUUOUUELZUUPUUAUAVNJZUUBUXBUYIUFZXFUU
      AUYBUYJUUBUYKXGQUAAUUJCUUEEFUVLUVMYBYCYDUUCUYDAUUOUUEUYHVMVLUSYFVMYGUUCUU
      RVNJZUWQUJUFUUAUUBUYLUUAUUBUUBYHUYJUYLXFAAUABUUJCUUEDEFUVLUVMGHYIYJUIUURY
      KWBVLYGUAXFYLWLWMUUCUWKUVNUVJUFUWPUVJYMWBYNVMUUCUWMUVKUVIUFZUWOUWMUWLUDUJ
      YOUYMUWNUDWNYPYQUVIUDYRYSWBYT $.
      $( [27-Nov-2007] $) $( [1-Feb-2007] $)

    $( Norm expressed in terms of inner product. $)
    ipnm $p |- ( ( U e. NrmCVec /\ A e. X ) ->
                ( N ` A ) = ( sqr ` ( A P A ) ) ) $=
      ( cnv wcel wa co csqr cfv c2 cexp ipid fveq2d cr cc0 cle wbr wceq nvcl
      nvge0 sqrsq syl11anc eqtr2d ) CIJAEJKZAABLZMNADNZOPLZMNZUKUIUJULMABCDEFGH
      QRUIUKSJTUKUAUBUMUKUCACDEFGUDACDEFGUEUKUFUGUH $.
      $( [11-Sep-2007] $)
  $}

  ${
    $d k A $.  $d k B $.  $d k G $.  $d k x y z U $.  $d k W $.
    $d x y z k X $.
    ipcl.1 $e |- X = ( BaseSet ` U ) $.
    ipcl.7 $e |- P = ( .i ` U ) $.
    $( An inner product is a complex number. $)
    ipcl $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( A P B ) e. CC ) $=
      ( vk cnv wcel w3a co c1 c4 cfz ci cv cexp cns cfv cpv cnm c2 cmul csu
      cdiv cc eqid ipval wral wa cn cn0 elfznn nnnn0 axicn expcl mpan 3syl
      adantl ipval2lem4 sylan2 mulcl syl11anc r19.21aiva cuz cz cle wbr 1z
      eluz1i 4nn nnz ax-mp cmin clt cc0 ax1cn subidi 4pos eqbrtri wb 1nn
      nnlem1lt mp2an mpbir mpbir2an fsumcl wne 4re recni gt0ne0ii divcl
      mp3an23 eqeltrd ) DIJAEJBEJKZABCLMNOLZPHQZRLZAWSBDSTZLDUATZLDUBTZTUCRLZUD
      LZHUEZNUFLZUGABCWTDHXAXBEFXAUHZWTUHZXBUHZGUIWPXDUGJZHWQUJZXEUGJZXFUGJZWPX
      JHWQWPWRWQJZUKWSUGJZXCUGJZXJXNXOWPXNWRULJWRUMJZXOWRNUNWRUOPUGJXQXOUPPWRUQ
      URUSZUTXNWPXOXPXRABWSCWTDXAXBEFXGXHXIGVAVBWSXCVCVDVENMVFTJZXKXLXSNVGJZMNV
      HVIZMNVJVKNULJZXTVLNVMVNYAMMVOLZNVPVIZYCVQNVPMVRVSVTWAMULJYBYAYDWBWCVLMNW
      DWEWFWGXDHMNWHURXLNUGJNVQWIXMNWJWKNWJVTWLXENWMWNUSWO $.
      $( [6-Oct-2007] $) $( [1-Feb-2007] $)

    $( Mapping for the inner product operation. $)
    ipf $p |- ( U e. NrmCVec -> P : ( X X. X ) --> CC ) $=
      ( vx vy vz vk cnv wcel cxp cc wf cv wa c1 c4 cfz co ci cexp cns cfv cpv
      cnm c2 cmul csu cdiv wceq copab2 wral w3a eqid ipval ipcl eqeltrrd
      3expib r19.21aivv foprab2 sylib ipfval feq1d mpbird ) BJKZCCLZMANVGMFOZCK
      ZGOZCKZPHOQRSTUAIOUBTZVHVLVJBUCUDZTBUEUDZTBUFUDZUDUGUBTUHTIUIRUJTZUKPFGHU
      LZNZVFVPMKZGCUMFCUMVRVFVSFGCCVFVIVKVSVFVIVKUNVHVJATVPMVHVJAVMBIVNVOCDVNUO
      ZVMUOZVOUOZEUPVHVJABCDEUQURUSUTFGHCCVPMVQVQUOVAVBVFVGMAVQFGHAVMBIVNVOCDVT
      WAWBEVCVDVE $.
      $( [28-Jan-2008] $)

    $( The complex conjugate of an inner product reverses its arguments.
       Equation I1 of [Ponnusamy] p. 362. $)
    ipcj $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( * ` ( A P B ) ) = ( B P A ) ) $=
      ( cnv wcel w3a cpv cfv co cnm c2 cexp c1 cneg cns cmin ci cmul caddc c4
      cdiv ccj cc wceq eqid ipval2lem3 recnd ax1cn negcli ipval2lem4 mpan2
      subcl syl11anc axicn mulcl mpan syl addcl cc0 wne 4re recni 4pos
      gt0ne0ii cjdiv mp3an23 cr cjre ax-mp opreq2i ipval2lem2 resubcl cjreim
      submul2 mp3an2 nvcom fveq2d opreq1d nvdif opreq12d negsubdi2 nvpi 3com23
      eqcomd eqtrd opreq2d 3eqtrd syl5eq ipval2 3eqtr4d ) DHIZAEIZBEIZJZABDKLZM
      ZDNLZLZOPMZAQRZBDSLZMWSMXALZOPMZTMZUAAUABXEMWSMXALZOPMZAUARZBXEMWSMXALZOP
      MZTMZUBMZUCMZUDUEMZUFLZBAWSMZXALZOPMZBXDAXEMWSMXALZOPMZTMZUABUAAXEMWSMXAL
      ZOPMZBXKAXEMWSMXALZOPMZTMZUBMZUCMZUDUEMZABCMZUFLBACMZWRXRXPUFLZUDUFLZUEMZ
      YLWRXPUGIZXRYQUHZWRXHUGIZXOUGIZYRWRXCUGIXGUGIZYTWRXCABCXEDWSXAEFWSUIZXEUI
      ZXAUIZGUJZUKWRXDUGIZUUBQULUMZABXDCXEDWSXAEFUUCUUDUUEGUNUOXCXGUPUQZWRXNUGI
      ZUUAWRXJUGIZXMUGIZUUJWRUAUGIZUUKURABUACXEDWSXAEFUUCUUDUUEGUNUOZWRXKUGIZUU
      LUAURUMZABXKCXEDWSXAEFUUCUUDUUEGUNUOZXJXMUPUQZUUMUUJUUAURUAXNUSUTVAXHXOVB
      UQYRUDUGIUDVCVDYSUDVEVFUDVEVGVHXPUDVIVJVAWRYQYOUDUEMYLYPUDYOUEUDVKIYPUDUH
      VEUDVLVMVNWRYOYKUDUEWRYOXHXOTMZXHUAXNRZUBMZUCMZYKWRXHVKIZXNVKIZYOUUSUHWRX
      CVKIXGVKIZUVCUUFWRUUGUVEUUHABXDCXEDWSXAEFUUCUUDUUEGVOUOXCXGVPUQWRXJVKIZXM
      VKIZUVDWRUUMUVFURABUACXEDWSXAEFUUCUUDUUEGVOUOWRUUOUVGUUPABXKCXEDWSXAEFUUC
      UUDUUEGVOUOXJXMVPUQXHXNVQUQWRYTUUJUUSUVBUHZUUIUURYTUUMUUJUVHURXHUAXNVRVSU
      QWRXHYDUVAYJUCWRXCYAXGYCTWRXBXTOPWRWTXSXAABDWSEFUUCVTWAWBWRXFYBOPABXEDWSX
      AEFUUCUUDUUEWCWBWDWRUUTYIUAUBWRUUTXMXJTMZYIWRUUKUULUUTUVIUHUUNUUQXJXMWEUQ
      WRXMYFXJYHTWRXLYEOPWRYEXLWOWQWPYEXLUHBAXEDWSXAEFUUCUUDUUEWFWGWHWBWRXIYGOP
      ABXEDWSXAEFUUCUUDUUEWFWBWDWIWJWDWKWBWLWIWRYMXQUFABCXEDWSXAEFUUCUUDUUEGWMW
      AWOWQWPYNYLUHBACXEDWSXAEFUUCUUDUUEGWMWGWN $.
      $( [30-Oct-2007] $) $( [1-Feb-2007] $)

    $( An inner product times its conjugate. $)
    ipipcj $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( ( A P B ) x. ( B P A ) ) = ( ( abs ` ( A P B ) ) ^ 2 ) ) $=
      ( cnv wcel w3a co cabs cfv c2 cexp ccj cmul cc wceq ipcl absvalsq syl
      ipcj opreq2d eqtr2d ) DHIAEIBEIJZABCKZLMNOKZUGUGPMZQKZUGBACKZQKUFUGRIUHUJ
      SABCDEFGTUGUAUBUFUIUKUGQABCDEFGUCUDUE $.
      $( [23-Nov-2007] $)

    $( Orthogonality (meaning inner product is 0) is commutative. $)
    iporthcom $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X )
              -> ( ( A P B ) = 0 <-> ( B P A ) = 0 ) ) $=
      ( cnv wcel w3a co cc0 wceq ccj cfv fveq2 cj0 syl6eq ipcj eqeq1d syl5ib
      3com23 impbid ) DHIZAEIZBEIZJZABCKZLMZBACKZLMZUIUHNOZLMUGUKUIULLNOZLUHLNP
      QRUGULUJLABCDEFGSTUAUKUJNOZLMUGUIUKUNUMLUJLNPQRUGUNUHLUDUFUEUNUHMBACDEFGS
      UBTUAUC $.
      $( [17-Apr-2008] $)
  $}

  ${
    ip0r.1 $e |- X = ( BaseSet ` U ) $.
    ip0r.5 $e |- Z = ( 0vec ` U ) $.
    ip0r.7 $e |- P = ( .i ` U ) $.
    $( Inner product with a zero second argument. $)
    ip0r $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( A P Z ) = 0 ) $=
      ( cnv wcel wa co cpv cfv cnm c2 cexp c1 cneg cns cmin ci cmul caddc c4
      cdiv cc0 wceq nvzcl adantr eqid ipval2 mpd3an3 cc ax1cn negcli nvsz
      mpan2 opreq2d fveq2d opreq1d cr ipval2lem3 recnd subid syl eqtrd axicn
      eqtr4d w3a ipval2lem4 opreq12d mul01i opreq2i 0cn addid1i eqtri syl6eq
      4re recni 4pos gt0ne0ii div0i ) CIJZADJZKZAEBLZAECMNZLZCONZNZPQLZARSZECTN
      ZLZWHLZWJNZPQLZUALZUBAUBEWNLZWHLZWJNZPQLZAUBSZEWNLZWHLZWJNZPQLZUALZUCLZUD
      LZUEUFLZUGWDWEEDJZWGXLUHWDXMWECDEFGUIUJZAEBWNCWHWJDFWHUKZWNUKZWJUKZHULUMW
      FXLUGUEUFLUGWFXKUGUEUFWFXKUGUBUGUCLZUDLZUGWFWSUGXJXRUDWFWSWLWLUALZUGWFWRW
      LWLUAWFWQWKPQWFWPWIWJWFWOEAWHWDWOEUHZWEWDWMUNJYARUOUPWMWNCEXPGUQURUJUSUTV
      AUSWFWLUNJXTUGUHWFWLWDWEXMWLVBJXNAEBWNCWHWJDFXOXPXQHVCUMVDWLVEVFVGWFXIUGU
      BUCWFXIXCXCUALZUGWFXHXCXCUAWFXGXBPQWFXFXAWJWFXEWTAWHWDXEWTUHWEWDXEEWTWDXD
      UNJXEEUHUBVHUPXDWNCEXPGUQURWDUBUNJZWTEUHVHUBWNCEXPGUQURVIUJUSUTVAUSWFXCUN
      JZYBUGUHWDWEXMYDXNWDWEXMVJYCYDVHAEUBBWNCWHWJDFXOXPXQHVKURUMXCVEVFVGUSVLXS
      UGUGUDLUGXRUGUGUDUBVHVMVNUGVOVPVQVRVAUEUEVSVTUEVSWAWBWCVRVG $.
      $( [1-Nov-2007] $) $( [5-Feb-2007] $)

    $( Inner product with a zero first argument.  Part of proof of Theorem 6.44
       of [Ponnusamy] p. 361. $)
    ip0l $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( Z P A ) = 0 ) $=
      ( cnv wcel wa co ccj cfv cc0 wceq nvzcl adantr ipcj mpd3an3 ip0r fveq2d
      cj0 syl6eq eqtr3d ) CIJZADJZKZAEBLZMNZEABLZOUFUGEDJZUJUKPUFULUGCDEFGQRAEB
      CDFHSTUHUJOMNOUHUIOMABCDEFGHUAUBUCUDUE $.
      $( [2-Nov-2007] $) $( [5-Feb-2007] $)

    $( The inner product of a vector with itself is zero iff the vector is
       zero.  Part of Definition 3.1-1 of [Kreyszig] p. 129. $)
    ipz $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( ( A P A ) = 0 <-> A = Z ) ) $=
      ( cnv wcel wa co cc0 wceq cnm cfv c2 cexp eqid ipid eqeq1d cc wb nvcl
      recnd sqeq0 syl nvz 3bitrd ) CIJADJKZAABLZMNACOPZPZQRLZMNZUMMNZAENUJUKUNM
      ABCULDFULSZHTUAUJUMUBJUOUPUCUJUMACULDFUQUDUEUMUFUGACULDEFGUQUHUI $.
      $( [24-Jan-2008] $)
  $}

  ${
    $d a b c d f k t u v w A $.  $d a u w C $.  $d a c d f u w D $.
    $d a b c d f k t u v w G $.  $d v w H $.  $d a k u w J $.  $d a k u w K $.
    $d a b c d f k t u v w N $.  $d a b c d f k t u v w S $.
    $d a b k t u v w U $.  $d a b c d f k t u v w X $.
    ip1cni.1 $e |- X = ( BaseSet ` U ) $.
    ip1cni.2 $e |- G = ( +v ` U ) $.
    ip1cni.7 $e |- P = ( .i ` U ) $.
    ip1cni.8 $e |- C = ( IndMet ` U ) $.
    ip1cni.d $e |- D = ( abs o. - ) $.
    ip1cni.j $e |- J = ( MetOpen ` C ) $.
    ip1cni.k $e |- K = ( MetOpen ` D ) $.
    ip1cni.f $e |- F = { <. w , v >. | ( w e. X /\ v = ( w P A ) ) } $.
    ip1cni.9 $e |- U e. NrmCVec $.
    ip1cni.a $e |- A e. X $.
    ${
      ip1cnilem.4 $e |- S = ( .s ` U ) $.
      ip1cnilem.6 $e |- N = ( norm ` U ) $.
      ${
        ip1cnilem.12 $e |- H = { <. u , t >. | ( u e. X /\ t =
          ( u G ( ( _i ^ k ) S A ) ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem1 $p |- ( k e. NN -> H e. ( J Cn J ) ) $=
          ( cv cn wcel ci cexp co cc ccn cn0 nnnn0 axicn expcl mpan syl cnv
          nvscl mp3an13 va1cn 3syl ) KULZUMUNZUOVKUPUQZURUNZVMEIUQZRUNZNOOUSUQU
          NVLVKUTUNZVNVKVAUOURUNVQVNVBUOVKVCVDVEJVFUNVNERUNVPUGUHVMEIJRSUIVGVHC
          DVOFJNMORSTUBUDUKUGVIVJ $.
          $( [13-Aug-2007] $)
      $}

      ${
        ip1cnilem.13 $e |- H = { <. u , t >. | ( u e. X /\ t =
          ( N ` ( u G ( ( _i ^ k ) S A ) ) ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem2 $p |- ( k e. NN -> H e. ( J Cn K ) ) $=
          ( va vb cv cn wcel ci cexp co wceq wa copab ccom ccn crn wss wral
          cn0 cc nnnn0 axicn expcl mpan cnv nvscl mp3an13 3syl nvgcl mp3an1
          sylan2 ancoms r19.21aiva eqid oprex rnssopab sylib cfv fvex fveq2
          wfn cr wf nvf ax-mp ffn dffn5 mpbi fopabco syl ip1cnilem1 nmcnc cme
          w3a imsmet cnmet 3pm3.2i metcnco sylancl eqeltrrd ) KUNZUOUPZQCUNZRUP
          ZDUNXLUQXJURUSZEIUSZMUSZUTVACDVBZVCZNOPVDUSZXKXQVERVFZXRNUTXKXPRUPZCR
          VGXTXKYACRXMXKYAXKXMXORUPZYAXKXJVHUPZXNVIUPZYBXJVJUQVIUPYCYDVKUQXJVLV
          MJVNUPZYDERUPYBUGUHXNEIJRSUIVOVPVQYEXMYBYAUGXLXOJMRSTVRVSVTWAWBCDRRXP
          XQXQWCZXLXOMWDZWEWFCDULUMDRRXPULUNZQWGZXPQWGXQQNYGYHQWHXPQWHYHXPQWIYF
          QRWJZQYHRUPUMUNYIUTVAULUMVBUTRWKQWLZYJYEYKUGJQRSUJWMWNRWKQWOWNULUMRQW
          PWQUKWRWSXKXQOOVDUSUPZQXSUPZXRXSUPZABCDEFGHIJKLMXQOPQRSTUAUBUCUDUEUFU
          GUHUIUJYFWTYEYMUGFGJOPQUJUBUCUDUEXAWNFXBUPZYOGXBUPZXCYLYMVAYNYOYOYPYE
          YOUGFJUBXDWNZYQGUCXEXFFFGXQQOOPUDUDUEXGVMXHXI $.
          $( [21-Aug-2007] $)
      $}

      ${
        ip1cnilem.14 $e |- H = { <. u , t >. | ( u e. X /\ t =
          ( ( N ` ( u G ( ( _i ^ k ) S A ) ) ) ^ 2 ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem3 $p |- ( k e. NN -> H e. ( J Cn K ) ) $=
          ( va vb cv cn wcel cc c2 cexp co wceq wa copab ci cfv ccom ccn crn
          wss wral cn0 nnnn0 axicn expcl mpan cnv nvscl mp3an13 3syl nvgcl
          mp3an1 sylan2 ancoms cr nvcl recnd syl r19.21aiva eqid fvex rnssopab
          sylib oprex opreq1 fopabco ip1cnilem2 sqcn2 cme w3a imsmet ax-mp
          cnmet 3pm3.2i metcnco sylancl eqeltrrd ) KUNZUOUPZULUNZUQUPUMUNXIURUS
          UTZVAVBULUMVCZCUNZRUPZDUNXLVDXGUSUTZEIUTZMUTZQVEZVAVBCDVCZVFZNOPVGUTZ
          XHXRVHUQVIZXSNVAXHXQUQUPZCRVJYAXHYBCRXHXMVBXPRUPZYBXMXHYCXHXMXORUPZYC
          XHXGVKUPZXNUQUPZYDXGVLVDUQUPYEYFVMVDXGVNVOJVPUPZYFERUPYDUGUHXNEIJRSUI
          VQVRVSYGXMYDYCUGXLXOJMRSTVTWAWBWCYCXQYGYCXQWDUPUGXPJQRSUJWEVOWFWGWHCD
          RUQXQXRXRWIZXPQWJZWKWLCDULUMDRUQXQXJXQURUSUTXRXKNYIXIURUSWMXQURUSWMXI
          XQURUSWNYHXKWIZUKWOWGXHXRXTUPZXKPPVGUTUPZXSXTUPZABCDEFGHIJKLMXROPQRST
          UAUBUCUDUEUFUGUHUIUJYHWPULUMGXKPUCUEYJWQFWRUPZGWRUPZYOWSYKYLVBYMYNYOY
          OYGYNUGFJUBWTXAGUCXBZYPXCFGGXRXKOPPUDUEUEXDVOXEXF $.
          $( [21-Aug-2007] $)
      $}

      ${
        ip1cnilem.15 $e |- H = { <. u , t >. |
              ( u e. X /\ t = ( ( ( _i ^ k ) x. ( 1 / 4 ) )
              x. ( ( N ` ( u G ( ( _i ^ k ) S A ) ) ) ^ 2 ) ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem4 $p |- ( k e. NN -> H e. ( J Cn K ) ) $=
          ( va vb vc vd vf cv cn wcel ci cexp co c1 c4 cdiv cmul cc cfv c2
          wceq wa copab ccn cn0 nnnn0 axicn expcl mpan 4re recni 4pos gt0ne0ii
          reccli mulcl mpan2 3syl eqid ip1cnilem3 cxp c1st c2nd cpr cr clt
          csup copab2 copn imsbai cnmetba cnv cme imsmet ax-mp cnmet mulcn
          opreq1 fveq2d opreq1d oprex fvopab4 opreq2d eqeq2d pm5.32i opabbii
          eqtr4i opr2cn syl11anc ) KUQZURUSZUTXRVAVBZVCVDVEVBZVFVBZVGUSZULUQZRU
          SUMUQYDXTEIVBZMVBZQVHZVIVAVBZVJVKULUMVLZOPVMVBZUSNYJUSXSXRVNUSZXTVGUS
          ZYCXRVOUTVGUSYKYLVPUTXRVQVRYLYAVGUSYCVDVDVSVTVDVSWAWBWCXTYAWDWEWFABUL
          UMEFGHIJKLMYIOPQRSTUAUBUCUDUEUFUGUHUIUJYIWGZWHUNUOUPCDFGGUNUQZVGVGWIZ
          USUOUQZYOUSVKUPUQYNWJVHYPWJVHGVBYNWKVHYPWKVHGVBWLWMWNWOVJVKUNUOUPWPZY
          BPYINGOPPYQWQVHZVFRVGVGFJRSUBUGWRGUCWSZYSJWTUSFXAUSUGFJUBXBXCGUCXDZYT
          YTUDUEUEYRWGZUEYQWGZUNUOUPGYQPYRUCUUBUEUUAXENCUQZRUSZDUQZYBUUCYEMVBZQ
          VHZVIVAVBZVFVBZVJZVKZCDVLUUDUUEYBUUCYIVHZVFVBZVJZVKZCDVLUKUUOUUKCDUUD
          UUNUUJUUDUUMUUIUUEUUDUULUUHYBVFULUMUUCYHUUHRYIYDUUCVJZYGUUGVIVAUUPYFU
          UFQYDUUCYEMXFXGXHYMUUGVIVAXIXJXKXLXMXNXOXPXQ $.
          $( [21-Aug-2007] $)
      $}

      ${
        ip1cnilem.16 $e |- H = { <. u , t >. |
               ( u e. X /\ t = ( ( ( _i ^ k ) x. ( ( N ` ( u G (
                 ( _i ^ k ) S A ) ) ) ^ 2 ) ) x. ( 1 / 4 ) ) ) } $.
        $( Lemma for ~ ip1cni . $)
        ip1cnilem5 $p |- ( k e. NN -> H e. ( J Cn K ) ) $=
          ( cv cn wcel ci cexp co c1 c4 cdiv cmul cfv c2 wceq wa copab ccn cc
          cn0 axicn nnnn0 expcl sylancr adantr cnv cr mpan nvscl mp3an13 3syl
          nvgcl mp3an1 sylan2 ancoms nvcl recnd sqcl syl 4re recni 4pos
          gt0ne0ii reccli mul32 mp3an3 syl11anc eqeq2d pm5.32da opabbidv
          syl5eq eqid ip1cnilem4 eqeltrd ) KULZUMUNZNCULZRUNZDULZUOXDUPUQZURUSU
          TUQZVAUQXFXIEIUQZMUQZQVBZVCUPUQZVAUQZVDZVEZCDVFZOPVGUQXENXGXHXIXNVAUQ
          XJVAUQZVDZVEZCDVFXRUKXEYAXQCDXEXGXTXPXEXGVEZXSXOXHYBXIVHUNZXNVHUNZXSX
          OVDZXEYCXGXEUOVHUNZXDVIUNZYCVJXDVKZUOXDVLZVMVNYBXMVHUNYDYBXMYBJVOUNZX
          LRUNZXMVPUNUGXGXEYKXEXGXKRUNZYKXEYGYCYLYHYFYGYCVJYIVQYJYCERUNYLUGUHXI
          EIJRSUIVRVSVTYJXGYLYKUGXFXKJMRSTWAWBWCWDXLJQRSUJWEVMWFXMWGWHYCYDXJVHU
          NYEUSUSWIWJUSWIWKWLWMXIXNXJWNWOWPWQWRWSWTABCDEFGHIJKLMXROPQRSTUAUBUCU
          DUEUFUGUHUIUJXRXAXBXC $.
          $( [21-Aug-2007] $)

        $( Lemma for ~ ip1cni . $)
        ip1cnilem6 $p |- F e. ( J Cn K ) $=
          ( c4 cn wcel ccn co 4nn cnv cme imsmet ax-mp imsbai ip1cnilem5 cv
          wceq wa copab c1 cfz cfv csu ci cexp c2 cmul cdiv cc cuz wral elnnuz
          mpbi elfznn cn0 axicn nnnn0 expcl sylancr adantl cr nvscl mp3an13
          syl nvgcl mp3an1 sylan2 nvcl recnd sqcl mulcl syl11anc r19.21aiva
          fsumcl cc0 wne 4re recni 4pos gt0ne0ii divrec mp3an23 ipval opreq1
          fveq2d opreq1d opreq2d oprex fvopab4 sumeq2sdv reccli fsummulc2
          mp3an12 eqtr4d 3eqtr4rd eqeq2d pm5.32i opabbii eqtr4i fsumcn ) ULUMUN
          ZLOPUOUPUNUQABFGKNLOPULRJURUNZFUSUNUGFJUBUTVAFJRSUBUGVBUCUDUEABCDEFGH
          IJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKVCLAVDZRUNZBVDZYKEHUPZVEZVFZABVGYLY
          MVHULVIUPZYKNVJZKVKZVEZVFZABVGUFUUAYPABYLYTYOYLYSYNYMYLYQVLKVDZVMUPZY
          KUUCEIUPZMUPZQVJZVNVMUPZVOUPZKVKZULVPUPZUUIVHULVPUPZVOUPZYNYSYLUUIVQU
          NZUUJUULVEZYLULVHVRVJUNZUUHVQUNZKYQVSZUUMYIUUOUQULVTWAZYLUUPKYQUUBYQU
          NYLUUBUMUNZUUPUUBULWBYLUUSVFZUUCVQUNZUUGVQUNZUUPUUSUVAYLUUSVLVQUNUUBW
          CUNUVAWDUUBWEVLUUBWFWGZWHUUTUUFVQUNUVBUUTUUFUUTYJUUERUNZUUFWIUNUGUUSY
          LUUDRUNZUVDUUSUVAUVEUVCYJUVAERUNZUVEUGUHUUCEIJRSUIWJWKWLYJYLUVEUVDUGY
          KUUDJMRSTWMWNWOUUEJQRSUJWPWGWQUUFWRWLUUCUUGWSWTWOXAZUUHKVHULXBWGUUMUL
          VQUNULXCXDUUNULXEXFZULXEXGXHZUUIULXIXJWLYJYLUVFYNUUJVEUGUHYKEHIJKMQRS
          TUIUJUAXKWKYLYSYQUUHUUKVOUPZKVKZUULYLYQYRUVJKCDYKUUCCVDZUUDMUPZQVJZVN
          VMUPZVOUPZUUKVOUPUVJRNUVLYKVEZUVPUUHUUKVOUVQUVOUUGUUCVOUVQUVNUUFVNVMU
          VQUVMUUEQUVLYKUUDMXLXMXNXOXNUKUUHUUKVOXPXQXRYLUUQUULUVKVEZUVGUUOUUKVQ
          UNUUQUVRUURULUVHUVIXSUUHUUKKVHULXTYAWLYBYCYDYEYFYGYHVA $.
          $( [21-Aug-2007] $)
      $}
    $}

    $( Inner product is continuous in its first operand. $)
    ip1cni $p |- F e. ( J Cn K ) $=
      ( vu vt vk cns cfv cv wcel ci cexp co cnm c2 cmul c1 c4 cdiv wceq wa
      copab eqid ip1cnilem6 ) ABUCUDCDEFGUFUGZGUEHIUCUHZLUIUDUHUJUEUHUKULZVEVFC
      VDULIULGUMUGZUGUNUKULUOULUPUQURULUOULUSUTUCUDVAZJKVGLMNOPQRSTUAUBVDVBVGVB
      VHVBVC $.
      $( [21-Aug-2007] $)
  $}

