$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                            Inner product
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c .i $.

  $( Extend class notation with the class inner product functions. $)
  cip $a class .i $.

  ${
    $d k n p u w x y z $.
    $( Define a function that maps a complex normed vector space to its inner
       product operation in case its norm satisfies the parallelogram identity
       (otherwise the operation is still defined, but not meaningful).  Based
       on Exercise 4(a) of [ReedSimon] p. 63 and Theorem 6.44 of [Ponnusamy]
       p. 361.  Vector addition is ` ( 1st `` w ) ` , the scalar product is
       ` ( 2nd `` w ) ` , and the norm is ` n ` . $)
    df-ip $a |- .i = ( u e. NrmCVec |->
      ( x e. ( BaseSet ` u ) , y e. ( BaseSet ` u ) |->
    ( sum_ k e. ( 1 ... 4 ) ( ( _i ^ k ) x. ( ( ( norm ` u ) ` ( x ( +v ` u )
        ( ( _i ^ k ) ( .s ` u ) y ) ) ) ^ 2 ) ) / 4 ) ) ) $.

$(
    df-ip $a |- .i = { <. x , y >. | ( x e. NrmCVec /\ E. g E. s E. n
            ( x = <. <. g , s >. , n >. /\ y = { <. <. a , b >. , c >. |
            ( ( a e. dom n /\ b e. dom n ) /\ c = ( sum_ k e. ( 1 ... 4 )
             ( ( _i ^ k ) x. ( ( n ` ( a g ( ( _i ^ k ) s b ) ) ) ^ 2 ) )
              / 4 ) ) } ) ) } $.

    df-ip $a |- .i = { <. x , y >. | ( x e. NrmCVec /\ E. g E. s E. n
            ( x = <. <. g , s >. , n >. /\ y = { <. <. a , b >. , c >. |
            ( ( a e. dom n /\ b e. dom n ) /\ c =
                ( ( ( ( ( n ` ( a g b ) ) ^ 2 ) +
                ( ( n ` ( a g ( -u 1 s b ) ) ) ^ 2 ) ) +
                ( _i x. ( ( ( n ` ( a g ( _i s b ) ) ) ^ 2 ) +
                ( ( n ` ( a g ( -u _i s b ) ) ) ^ 2 ) ) ) ) / 4 ) ) } ) ) } $.
$)
  $}

  ${
    $d k u x y G $.  $d k u x y N $.  $d k u x y S $.  $d k u x y U $.
    $d k x y A $.  $d k y B $.  $d k u x y X $.
    ipfval.1 $e |- X = ( BaseSet ` U ) $.
    ipfval.2 $e |- G = ( +v ` U ) $.
    ipfval.4 $e |- S = ( .s ` U ) $.
    ipfval.6 $e |- N = ( norm ` U ) $.
    ipfval.7 $e |- P = ( .i ` U ) $.
    $( The inner product function on a normed complex vector space.  The
       definition is meaningful for vector spaces that are also inner product
       spaces, i.e. satisfy the parallelogram law. $)
    ipfval $p |- ( U e. NrmCVec -> P = ( x e. X , y e. X |->
      ( sum_ k e. ( 1 ... 4 ) ( ( _i ^ k ) x.
        ( ( N ` ( x G ( ( _i ^ k ) S y ) ) ) ^ 2 ) ) / 4 ) ) ) $=
      ( cfv c4 co cv cexp cvv vu cnv wcel cip c1 cfz ci cmul csu cdiv cmpt2 cba
      c2 cns cpv cnm wceq fveq2 syl6eqr eqidd opreq123d fveq12d opreq1d opreq2d
      opreqd sumeq2sdv mpt2eq123dv df-ip eqeltri mpt2exga mp2an fvmpt syl5eq
      fvex ) EUBUCCEUDOABIIUEPUFQZUGFRSQZARZVPBRZDQZGQZHOZUMSQZUHQZFUIZPUJQZUKZ
      NUAEABUARZULOZWHVOVPVQVPVRWGUNOZQZWGUOOZQZWGUPOZOZUMSQZUHQZFUIZPUJQZUKWFU
      BUDWGEUQZABWHWHWRIIWEWSWHEULOZIWGEULURJUSZXAWSWQWDPUJWSVOWPWCFWSWOWBVPUHW
      SWNWAUMSWSWLVTWMHWSWMEUPOHWGEUPURMUSWSVQVQWJVSWKGWSWKEUOOGWGEUOURKUSWSVQU
      TWSWIDVPVRWSWIEUNODWGEUNURLUSVEVAVBVCVDVFVCVGABUAFVHITUCZXBWFTUCIWTTJEULV
      NVIZXCABIIWETTVJVKVLVM $.
      $( [16-Nov-2013] $) $( [10-Apr-2007] $)

    $( Value of the inner product.  The definition is meaningful for normed
       complex vector spaces that are also inner product spaces, i.e. satisfy
       the parallelogram law, although for convenience we define it for any
       normed complex vector space.  The vector (group) addition operation is
       ` G ` , the scalar product is ` S ` , the norm is ` N ` , and the set of
       vectors is ` X ` .  Equation 6.45 of [Ponnusamy] p. 361. $)
    ipval $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B ) =
                ( sum_ k e. ( 1 ... 4 ) ( ( _i ^ k ) x. ( ( N ` ( A G
                    ( ( _i ^ k ) S B ) ) ) ^ 2 ) ) / 4 ) ) $=
      ( co c4 cexp c2 cmul cdiv vx vy cnv wcel c1 cfz ci cv cfv csu wceq ipfval
      wa cmpt2 opreqd opreq1 fveq2d opreq1d opreq2d sumeq2sdv opreq2 eqid oprex
      ovmpt2 sylan9eq 3impb ) EUCUDZAIUDZBIUDZABCOZUEPUFOZUGFUHQOZAVLBDOZGOZHUI
      ZRQOZSOZFUJZPTOZUKVGVHVIUMVJABUAUBIIVKVLUAUHZVLUBUHZDOZGOZHUIZRQOZSOZFUJZ
      PTOZUNZOVSVGCWIABUAUBCDEFGHIJKLMNULUOUAUBABIIWHVSWIVKVLAWBGOZHUIZRQOZSOZF
      UJZPTOVTAUKZWGWNPTWOVKWFWMFWOWEWLVLSWOWDWKRQWOWCWJHVTAWBGUPUQURUSUTURWABU
      KZWNVRPTWPVKWMVQFWPWLVPVLSWPWKVORQWPWJVNHWPWBVMAGWABVLDVAUSUQURUSUTURWIVB
      VRPTVCVDVEVF $.
      $( [16-Nov-2013] $) $( [31-Jan-2007] $)

    $( Lemma for ~ ipval3 . $)
    ipval2lem2 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
               ( ( N ` ( A G ( C S B ) ) ) ^ 2 ) e. RR ) $=
      ( cnv wcel w3a co cr adantr cc wa cfv cexp simp1 simp2 nvscl 3com23 3expa
      c2 3adantl2 nvgcl syl3anc nvcl syl2anc resqcl syl ) FOPZAIPZBIPZQZCUAPZUB
      ZACBERZGRZHUCZSPZVFUJUDRSPVCURVEIPZVGVAURVBURUSUTUETZVCURUSVDIPZVHVIVAUSV
      BURUSUTUFTURUTVBVJUSURUTVBVJURVBUTVJCBEFIJLUGUHUIUKAVDFGIJKULUMVEFHIJMUNU
      OVFUPUQ $.
      $( [1-Oct-2007] $) $( [1-Feb-2007] $)

    $( Lemma for ~ ipval3 . $)
    ipval2lem3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( ( N ` ( A G B ) ) ^ 2 ) e. RR ) $=
      ( wcel c1 co cfv c2 cexp cr cnv w3a wceq wa nvsid opreq2d opreq1d 3adant2
      fveq2d cc ax-1cn ipval2lem2 mpan2 eqeltrrd ) EUANZAHNZBHNZUBZAOBDPZFPZGQZ
      RSPZABFPZGQZRSPZTUOUQVBVEUCUPUOUQUDZVAVDRSVFUTVCGVFUSBAFBDEHIKUEUFUIUGUHU
      ROUJNVBTNUKABOCDEFGHIJKLMULUMUN $.
      $( [1-Oct-2007] $) $( [1-Feb-2007] $)

    $( Lemma for ~ ipval3 . $)
    ipval2lem4 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
               ( ( N ` ( A G ( C S B ) ) ) ^ 2 ) e. CC ) $=
      ( cnv wcel w3a cc wa co cfv c2 cexp ipval2lem2 recnd ) FOPAIPBIPQCRPSACBE
      TGTHUAUBUCTABCDEFGHIJKLMNUDUE $.
      $( [1-Oct-2007] $) $( [1-Feb-2007] $)

    $( Expansion of the inner product value ~ ipval .  (Revised by Mario
       Carneiro, 5-May-2014.) $)
    ipval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B )
               = ( ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) +
               ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u _i S B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
      ( wcel co c1 ci cexp c2 wceq vk cnv w3a c4 cfz cv cfv cmul cdiv cneg cmin
      csu caddc ipval cc ax-icn ipval2lem4 mpan2 mulcl mpan ax-1cn negcli subcl
      syl syl2anc negsub mulm1 opreq2d eqtrd mulneg1 subdi mp3an1 opreq1d sub32
      opreq12d syl3anc 3eqtr4d wa nvsid fveq2d 3adant2 ipval2lem3 recnd cn nnuz
      mulid2 c3 df-4 opreq2 i4 syl6eq nnnn0 expcl sylancr adantl sylan2 df-3 i3
      cn0 df-2 i2 cz 1z a1i exp1 ax-mp fsum1 1nn fsump1i simprd addcom subadd23
      jctil eqtr4d ) EUBNZAHNZBHNZUCZABCOPUDUEOQUAUFZROZAXTBDOZFOZGUGZSROZUHOZU
      AULZUDUIOABFOZGUGZSROZAPUJZBDOZFOZGUGZSROZUKOZQAQBDOZFOZGUGZSROZAQUJZBDOZ
      FOZGUGZSROZUKOZUHOZUMOZUDUIOABCDEUAFGHIJKLMUNXRYFUUGUDUIXRQYSUHOZYJYNUHOZ
      UMOZYTUUDUHOZUMOZPAPBDOZFOZGUGZSROZUHOZUMOZUUFYNUKOZYIUMOZYFUUGXRUULUUSUU
      QYIUMXRUUHYNUKOZQUUDUHOZUJZUMOZUVAUVBUKOZUULUUSXRUVAUONZUVBUONZUVDUVETXRU
      UHUONZYNUONZUVFXRYSUONZUVHXRQUONZUVJUPABQCDEFGHIJKLMUQURZUVKUVJUVHUPQYSUS
      ZUTVDZXRYJUONUVIPVAVBABYJCDEFGHIJKLMUQURZUUHYNVCVEXRUUDUONZUVGXRYTUONUVPQ
      UPVBABYTCDEFGHIJKLMUQURZUVKUVPUVGUPQUUDUSUTVDZUVAUVBVFVEXRUUJUVAUUKUVCUMX
      RUUJUUHYNUJZUMOZUVAXRUUIUVSUUHUMXRUVIUUIUVSTUVOYNVGVDVHXRUVHUVIUVTUVATUVN
      UVOUUHYNVFVEVIXRUVPUUKUVCTZUVQUVKUVPUWAUPQUUDVJUTVDVOXRUUSUUHUVBUKOZYNUKO
      ZUVEXRUUFUWBYNUKXRUVJUVPUUFUWBTZUVLUVQUVKUVJUVPUWDUPQYSUUDVKVLVEVMXRUVHUV
      GUVIUWCUVETUVNUVRUVOUUHUVBYNVNVPVIVQXRUUQPYIUHOZYIXRUUPYIPUHXOXQUUPYITXPX
      OXQVRZUUOYHSRUWFUUNYGGUWFUUMBAFBDEHIKVSVHVTVMWAVHXRYIUONZUWEYITXRYIABCDEF
      GHIJKLMWBWCZYIWFVDVIVOXRUDWDNYFUURTXRYEUUQUULUAWGPUDWDWEWHXSUDTZXTPYDUUPU
      HUWIXTQUDROPXSUDQRWIWJWKZUWIYCUUOSRUWIYBUUNGUWIYAUUMAFUWIXTPBDUWJVMVHVTVM
      VOXRXSWDNZVRXTUONZYDUONZYEUONUWKUWLXRUWKUVKXSWSNUWLUPXSWLQXSWMWNZWOUWKXRU
      WLUWMUWNABXTCDEFGHIJKLMUQWPXTYDUSVEZXRYEUUKUUJUASPWGWDWEWQXSWGTZXTYTYDUUD
      UHUWPXTQWGROYTXSWGQRWIWRWKZUWPYCUUCSRUWPYBUUBGUWPYAUUAAFUWPXTYTBDUWQVMVHV
      TVMVOUWOXRYEUUIUUHUAPPSWDWEWTXSSTZXTYJYDYNUHUWRXTQSROYJXSSQRWIXAWKZUWRYCY
      MSRUWRYBYLGUWRYAYKAFUWRXTYJBDUWSVMVHVTVMVOUWOXRPPUEOYEUAULUUHTZPWDNXRPXBN
      ZUVHUWTUXAXRXCXDXRUVKUVJUVHUPUVLUVMWNYEUUHUAPXSPTZXTQYDYSUHUXBXTQPROZQXSP
      QRWIUVKUXCQTUPQXEXFWKZUXBYCYRSRUXBYBYQGUXBYAYPAFUXBXTQBDUXDVMVHVTVMVOXGVE
      XHXMXIXIXIXJXRUUGUUFYOUMOZUUTXRYOUONZUUFUONZUUGUXETXRUWGUVIUXFUWHUVOYIYNV
      CVEXRUUEUONZUXGXRUVJUVPUXHUVLUVQYSUUDVCVEUVKUXHUXGUPQUUEUSUTVDZYOUUFXKVEX
      RUXGUVIUWGUUTUXETUXIUVOUWHUUFYNYIXLVPXNVQVMVI $.
      $( [1-Oct-2007] $) $( [31-Jan-2007] $)

    $( Four times the inner product value ~ ipval3 , useful for simplifying
       certain proofs. $)
    4ipval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
        ( 4 x. ( A P B ) ) = ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) +
               ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u _i S B ) ) ) ^ 2 ) ) ) ) ) $=
      ( wcel c4 co ci cc syl2anc syl cnv w3a cmul cfv c2 cexp c1 cneg cmin cdiv
      caddc ipval2 opreq2d wceq simp1 nvgcl nvcl recnd sqcl ax-1cn negcli nvscl
      cr mp3an2 3adant2 syld3an3 subcl ax-icn mulcl mpan addcl cc0 wne 4re 4pos
      recni gt0ne0ii divcan2 mp3an23 eqtrd ) EUANZAHNZBHNZUBZOABCPZUCPOABFPZGUD
      ZUEUFPZAUGUHZBDPZFPZGUDZUEUFPZUIPZQAQBDPZFPZGUDZUEUFPZAQUHZBDPZFPZGUDZUEU
      FPZUIPZUCPZUKPZOUJPZUCPZXFWDWEXGOUCABCDEFGHIJKLMULUMWDXFRNZXHXFUNZWDWNRNZ
      XERNZXIWDWHRNZWMRNZXKWDWGRNXMWDWGWDWAWFHNWGVCNWAWBWCUOZABEFHIJUPWFEGHILUQ
      SURWGUSTWDWLRNXNWDWLWDWAWKHNZWLVCNXOWAWBWCWJHNZXPWAWCXQWBWAWIRNWCXQUGUTVA
      WIBDEHIKVBVDVEAWJEFHIJUPVFWKEGHILUQSURWLUSTWHWMVGSWDXDRNZXLWDWRRNZXCRNZXR
      WDWQRNXSWDWQWDWAWPHNZWQVCNXOWAWBWCWOHNZYAWAWCYBWBWAQRNZWCYBVHQBDEHIKVBVDV
      EAWOEFHIJUPVFWPEGHILUQSURWQUSTWDXBRNXTWDXBWDWAXAHNZXBVCNXOWAWBWCWTHNZYDWA
      WCYEWBWAWSRNWCYEQVHVAWSBDEHIKVBVDVEAWTEFHIJUPVFXAEGHILUQSURXBUSTWRXCVGSYC
      XRXLVHQXDVIVJTWNXEVKSXIORNOVLVMXJOVNVPOVNVOVQXFOVRVSTVT $.
      $( [10-Apr-2007] $)

    ${
      ipval3.3 $e |- M = ( -v ` U ) $.
      $( Expansion of the inner product value ~ ipval . $)
      ipval3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B )
               = ( ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A M B ) ) ^ 2 ) ) +
               ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A M ( _i S B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
        ( wcel co c2 cexp ci cnv w3a cfv c1 cneg cmin cmul caddc c4 cdiv ipval2
        nvmval fveq2d opreq1d opreq2d wceq ax-icn nvscl mp3an2 3adant2 syld3an3
        cc wa mulm1i opreq1i ax-1cn negcli nvsass mp3anr1 mpanr1 syl5reqr eqtrd
        opreq12d eqtr4d ) EUAPZAIPZBIPZUBZABCQABFQHUCRSQZAUDUEZBDQFQZHUCZRSQZUF
        QZTATBDQZFQHUCRSQZATUEZBDQZFQZHUCZRSQZUFQZUGQZUHQZUIUJQVSABGQZHUCZRSQZU
        FQZTWFAWEGQZHUCZRSQZUFQZUGQZUHQZUIUJQABCDEFHIJKLMNUKVRXDWNUIUJVRWRWDXCW
        MUHVRWQWCVSUFVRWPWBRSVRWOWAHABDEFGIJKLOULUMUNUOVRXBWLTUGVRXAWKWFUFVRWTW
        JRSVRWSWIHVRWSAVTWEDQZFQZWIVOVPVQWEIPZWSXFUPVOVQXGVPVOTVBPZVQXGUQTBDEIJ
        LURUSUTAWEDEFGIJKLOULVAVRXEWHAFVOVQXEWHUPVPVOVQVCWHVTTUGQZBDQZXEXIWGBDT
        UQVDVEVOXHVQXJXEUPZUQVOVTVBPXHVQXKUDVFVGVTTBDEIJLVHVIVJVKUTUOVLUMUNUOUO
        VMUNVN $.
        $( [17-Nov-2007] $)

      $( Lemma for ~ ipval3 . $)
      ipval2lem5 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
                 ( ( N ` ( A M ( C S B ) ) ) ^ 2 ) e. RR ) $=
        ( wcel co cr adantr cnv w3a cc wa cfv c2 simp1 simp2 nvscl 3com23 3expa
        cexp 3adantl2 nvmcl syl3anc nvcl syl2anc resqcl syl ) FUAQZAJQZBJQZUBZC
        UCQZUDZACBERZHRZIUEZSQZVHUFULRSQVEUTVGJQZVIVCUTVDUTVAVBUGTZVEUTVAVFJQZV
        JVKVCVAVDUTVAVBUHTUTVBVDVLVAUTVBVDVLUTVDVBVLCBEFJKMUIUJUKUMAVFFHJKPUNUO
        VGFIJKNUPUQVHURUS $.
        $( [1-Feb-2008] $)

      $( Lemma for ~ ipval3 . $)
      ipval2lem6 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                 ( ( N ` ( A M B ) ) ^ 2 ) e. RR ) $=
        ( wcel c1 co c2 cexp cnv w3a cfv cr wceq opreq2d fveq2d opreq1d 3adant2
        wa nvsid cc ax-1cn ipval2lem5 mpan2 eqeltrrd ) EUAPZAIPZBIPZUBZAQBDRZGR
        ZHUCZSTRZABGRZHUCZSTRZUDUQUSVDVGUEURUQUSUJZVCVFSTVHVBVEHVHVABAGBDEIJLUK
        UFUGUHUIUTQULPVDUDPUMABQCDEFGHIJKLMNOUNUOUP $.
        $( [1-Feb-2008] $)

      $( Four times the inner product value ~ ipval3 , useful for simplifying
         certain proofs. $)
      4ipval3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
          ( 4 x. ( A P B ) ) = ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
                 ( ( N ` ( A M B ) ) ^ 2 ) ) +
                 ( _i x. ( ( ( N ` ( A G ( _i S B ) ) ) ^ 2 ) -
                 ( ( N ` ( A M ( _i S B ) ) ) ^ 2 ) ) ) ) ) $=
        ( wcel c4 co ci cc cnv w3a cmul cfv cexp cmin caddc cdiv ipval3 opreq2d
        c2 ipval2lem3 recnd ipval2lem6 subcl syl2anc cr ax-icn ipval2lem2 mpan2
        wceq ipval2lem5 mulcl mpan syl addcl cc0 wne 4re recni gt0ne0ii divcan2
        4pos mp3an23 eqtrd ) EUAPAIPBIPUBZQABCRZUCRQABFRHUDUKUERZABGRHUDUKUERZU
        FRZSASBDRZFRHUDUKUERZAWAGRHUDUKUERZUFRZUCRZUGRZQUHRZUCRZWFVPVQWGQUCABCD
        EFGHIJKLMNOUIUJVPWFTPZWHWFVAZVPVTTPZWETPZWIVPVRTPVSTPWKVPVRABCDEFHIJKLM
        NULUMVPVSABCDEFGHIJKLMNOUNUMVRVSUOUPVPWDTPZWLVPWBTPWCTPWMVPWBVPSTPZWBUQ
        PURABSCDEFHIJKLMNUSUTUMVPWCVPWNWCUQPURABSCDEFGHIJKLMNOVBUTUMWBWCUOUPWNW
        MWLURSWDVCVDVEVTWEVFUPWIQTPQVGVHWJQVIVJQVIVMVKWFQVLVNVEVO $.
        $( [1-Feb-2008] $)
    $}
  $}

  ${
    $d k n p w x y z G $.  $d k n p w x y z N $.  $d k n p w x y z S $.
    $d k n p w x y z U $.  $d k x y z A $.  $d k x y z B $.  $d x y z X $.
    ipid.1 $e |- X = ( BaseSet ` U ) $.
    ipid.6 $e |- N = ( norm ` U ) $.
    ipid.7 $e |- P = ( .i ` U ) $.
    $( The inner product of a vector with itself is the square of the vector's
       norm.  Equation I4 of [Ponnusamy] p. 362. $)
    ipidsq $p |- ( ( U e. NrmCVec /\ A e. X ) ->
                ( A P A ) = ( ( N ` A ) ^ 2 ) ) $=
      ( wcel co cfv c2 cexp c1 ci cmul caddc c4 wceq cc0 cnv cpv cneg cmin cdiv
      wa cns eqid ipval2 3anidm23 c1st cvc nvvc vafval smfval bafval vc2 fveq2d
      sylan cr cle wbr 2nn nnrei 0re 2re 2pos ltleii pm3.2i nvsge0 mp3an2 eqtrd
      opreq1d nvcl recnd cn0 2cn 2nn0 mulexp mp3an13 syl sq2 opreq1i a1i 3eqtrd
      cn0v nvrinv nvz0 adantr sq0 syl6eq opreq12d 4re recni sqcl sylancr subid1
      mulcl cabs csqr 1re renegcli absreim mp2an ax-icn ax-1cn mulneg2i mulid1i
      negeqi eqtri opreq2i fveq2i sqneg ax-mp 3eqtr3i 3eqtr2i negcli addcli nvs
      3eqtr4a nvdir mp3anr1 mpanr1 nvsid 3eqtr3d opreq2d ipval2lem4 mpan2 subid
      cc w3a mul01i addid1 eqtr2d wne 4pos gt0ne0ii divcan3 mp3an23 3eqtr2d ) C
      UAIZAEIZUFZAABJZAACUBKZJZDKZLMJZANUCZACUGKZJUUEJZDKZLMJZUDJZOAOAUUJJZUUEJ
      ZDKZLMJZAOUCZAUUJJZUUEJZDKZLMJZUDJZPJZQJZRUEJZRADKZLMJZPJZRUEJZUVIUUAUUBU
      UDUVGSAABUUJCUUEDEFUUEUHZUUJUHZGHUIUJUUCUVJUVFRUEUUCUVFUVJTQJZUVJUUCUUNUV
      JUVETQUUCUUNUVJTUDJZUVJUUCUUHUVJUUMTUDUUCUUHLUVHPJZLMJZLLMJZUVIPJZUVJUUCU
      UGUVPLMUUCUUGLAUUJJZDKZUVPUUCUUFUVTDUUACUKKZULIUUBUUFUVTSCUWBUWBUHUMAUUJU
      UEUWBECUUEUVLUNUUJCUVMUOCUUEEFUVLUPUQUSURUUALUTIZTLVAVBZUFUUBUWAUVPSUWCUW
      DLVCVDTLVEVFVGVHVILAUUJCDEFUVMGVJVKVLVMUUCUVHYJIZUVQUVSSZUUCUVHACDEFGVNVO
      ZLYJIUWELVPIUWFVQVRLUVHLVSVTWAUVSUVJSUUCUVRRUVIPWBWCWDWEUUCUUMTLMJTUUCUUL
      TLMUUCUULCWFKZDKZTUUCUUKUWHDAUUJCUUEEUWHFUVLUVMUWHUHZWGURUUAUWITSUUBCDUWH
      UWJGWHWIVLVMWJWKWLUUCUVJYJIZUVOUVJSUUCRYJIZUVIYJIZUWKRWMWNZUUCUWEUWMUWGUV
      HWOWAZRUVIWRWPZUVJWQWAVLUUCUVEOTPJTUUCUVDTOPUUCUVDUURUURUDJZTUUCUVCUURUUR
      UDUUCUVBUUQLMUUCNUUSQJZAUUJJZDKZNOQJZAUUJJZDKZUVBUUQUUCUWRWSKZUVHPJZUXAWS
      KZUVHPJZUWTUXCUXDUXFUVHPUXDNLMJZUXHQJZWTKZNONPJZQJZWSKZUXFNOUUIPJZQJZWSKZ
      UXHUUILMJZQJZWTKZUXDUXJNUTIZUUIUTIUXPUXSSXANXAXBNUUIXCXDUXOUWRWSUXNUUSNQU
      XNUXKUCUUSONXEXFXGUXKOOXEXHZXIXJXKXLUXRUXIWTUXQUXHUXHQNYJIZUXQUXHSXFNXMXN
      XKXLXOUXTUXTUXMUXJSXAXANNXCXDUXLUXAWSUXKONQUYAXKXLXPWCUUAUWRYJIUUBUWTUXES
      NUUSXFOXEXQZXRUWRAUUJCDEFUVMGXSVKUUAUXAYJIUUBUXCUXGSNOXFXEXRUXAAUUJCDEFUV
      MGXSVKXTUUCUWSUVADUUCUWSNAUUJJZUUTUUEJZUVAUUAUUSYJIZUUBUWSUYESZUYCUUAUYBU
      YFUUBUYGXFNUUSAUUJCUUEEFUVLUVMYAYBYCUUCUYDAUUTUUEAUUJCEFUVMYDZVMVLURUUCUX
      BUUPDUUCUXBUYDUUOUUEJZUUPUUAOYJIZUUBUXBUYISZXEUUAUYBUYJUUBUYKXFNOAUUJCUUE
      EFUVLUVMYAYBYCUUCUYDAUUOUUEUYHVMVLURYEVMYFUUCUURYJIZUWQTSUUAUUBUYLUUAUUBU
      UBYKUYJUYLXEAAOBUUJCUUEDEFUVLUVMGHYGYHUJUURYIWAVLYFOXEYLWKWLUUCUWKUVNUVJS
      UWPUVJYMWAYNVMUUCUWMUVKUVISZUWOUWMUWLRTYOUYMUWNRWMYPYQUVIRYRYSWAYT $.
      $( [27-Nov-2007] $) $( [1-Feb-2007] $)

    $( Norm expressed in terms of inner product. $)
    ipnm $p |- ( ( U e. NrmCVec /\ A e. X ) ->
                ( N ` A ) = ( sqr ` ( A P A ) ) ) $=
      ( cnv wcel wa co csqr cfv c2 cexp ipidsq fveq2d cr cc0 cle wbr wceq nvge0
      nvcl sqrsq syl2anc eqtr2d ) CIJAEJKZAABLZMNADNZOPLZMNZUKUIUJULMABCDEFGHQR
      UIUKSJTUKUAUBUMUKUCACDEFGUEACDEFGUDUKUFUGUH $.
      $( [11-Sep-2007] $)
  $}

  ${
    $d k A $.  $d k B $.  $d k x y U $.  $d k x y X $.
    ipcl.1 $e |- X = ( BaseSet ` U ) $.
    ipcl.7 $e |- P = ( .i ` U ) $.
    $( An inner product is a complex number.  (Revised by Mario Carneiro,
       5-May-2014.) $)
    ipcl $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( A P B ) e. CC ) $=
      ( vk cnv wcel w3a co c1 c4 ci cexp cfv cc eqid 4re cfz cv cns cpv c2 cmul
      cnm csu cdiv ipval fzfid wa cn elfznn nnnn0 ax-icn expcl mpan 3syl adantl
      cn0 ipval2lem4 sylan2 mulcl syl2anc fsumcl cc0 wne recni gt0ne0ii mp3an23
      4pos divcl syl eqeltrd ) DIJAEJBEJKZABCLMNUALZOHUBZPLZAVSBDUCQZLDUDQZLDUG
      QZQUEPLZUFLZHUHZNUILZRABCVTDHWAWBEFWASZVTSZWBSZGUJVPWERJZWFRJZVPVQWDHVPMN
      UKVPVRVQJZULVSRJZWCRJZWDRJWLWMVPWLVRUMJVRVAJZWMVRNUNVRUOORJWOWMUPOVRUQURU
      SZUTWLVPWMWNWPABVSCVTDWAWBEFWGWHWIGVBVCVSWCVDVEVFWJNRJNVGVHWKNTVINTVLVJWE
      NVMVKVNVO $.
      $( [5-May-2014] $) $( [1-Feb-2007] $)

    $( Mapping for the inner product operation. $)
    ipf $p |- ( U e. NrmCVec -> P : ( X X. X ) --> CC ) $=
      ( vx vy vk cnv wcel cxp cc wf c4 co cv cexp cfv wral eqid cfz cns cpv cnm
      c1 ci cmul csu cdiv cmpt2 w3a ipval eqeltrrd 3expib ralrimivv fmpt2 sylib
      c2 ipcl ipfval feq1d mpbird ) BIJZCCKZLAMVDLFGCCUENUAOUFHPQOZFPZVEGPZBUBR
      ZOBUCRZOBUDRZRURQOUGOHUHNUIOZUJZMZVCVKLJZGCSFCSVMVCVNFGCCVCVFCJZVGCJZVNVC
      VOVPUKVFVGAOVKLVFVGAVHBHVIVJCDVITZVHTZVJTZEULVFVGABCDEUSUMUNUOFGCCVKLVLVL
      TUPUQVCVDLAVLFGAVHBHVIVJCDVQVRVSEUTVAVB $.
      $( [16-Nov-2013] $) $( [28-Jan-2008] $)

    $( The complex conjugate of an inner product reverses its arguments.
       Equation I1 of [Ponnusamy] p. 362. $)
    ipcj $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( * ` ( A P B ) ) = ( B P A ) ) $=
      ( wcel cfv co c2 cexp cmin ci c4 cdiv cc wceq syl2anc cr cnv w3a cpv cneg
      cnm c1 cns cmul caddc ccj ipval2lem3 recnd ax-1cn negcli ipval2lem4 mpan2
      eqid subcl ax-icn mulcl mpan syl addcl cc0 wne 4re recni gt0ne0ii mp3an23
      cjdiv ax-mp opreq2i ipval2lem2 resubcl cjreim submul2 mp3an2 nvcom fveq2d
      4pos cjre opreq1d nvdif negsubdi2 nvpi 3com23 eqcomd eqtrd opreq2d 3eqtrd
      opreq12d syl5eq ipval2 3eqtr4d ) DUAHZAEHZBEHZUBZABDUCIZJZDUEIZIZKLJZAUFU
      DZBDUGIZJWSJXAIZKLJZMJZNANBXEJWSJXAIZKLJZANUDZBXEJWSJXAIZKLJZMJZUHJZUIJZO
      PJZUJIZBAWSJZXAIZKLJZBXDAXEJWSJXAIZKLJZMJZNBNAXEJWSJXAIZKLJZBXKAXEJWSJXAI
      ZKLJZMJZUHJZUIJZOPJZABCJZUJIBACJZWRXRXPUJIZOUJIZPJZYLWRXPQHZXRYQRZWRXHQHZ
      XOQHZYRWRXCQHXGQHZYTWRXCABCXEDWSXAEFWSUQZXEUQZXAUQZGUKZULWRXDQHZUUBUFUMUN
      ZABXDCXEDWSXAEFUUCUUDUUEGUOUPXCXGURSZWRXNQHZUUAWRXJQHZXMQHZUUJWRNQHZUUKUS
      ABNCXEDWSXAEFUUCUUDUUEGUOUPZWRXKQHZUULNUSUNZABXKCXEDWSXAEFUUCUUDUUEGUOUPZ
      XJXMURSZUUMUUJUUAUSNXNUTVAVBXHXOVCSYROQHOVDVEYSOVFVGOVFVTVHXPOVJVIVBWRYQY
      OOPJYLYPOYOPOTHYPORVFOWAVKVLWRYOYKOPWRYOXHXOMJZXHNXNUDZUHJZUIJZYKWRXHTHZX
      NTHZYOUUSRWRXCTHXGTHZUVCUUFWRUUGUVEUUHABXDCXEDWSXAEFUUCUUDUUEGVMUPXCXGVNS
      WRXJTHZXMTHZUVDWRUUMUVFUSABNCXEDWSXAEFUUCUUDUUEGVMUPWRUUOUVGUUPABXKCXEDWS
      XAEFUUCUUDUUEGVMUPXJXMVNSXHXNVOSWRYTUUJUUSUVBRZUUIUURYTUUMUUJUVHUSXHNXNVP
      VQSWRXHYDUVAYJUIWRXCYAXGYCMWRXBXTKLWRWTXSXAABDWSEFUUCVRVSWBWRXFYBKLABXEDW
      SXAEFUUCUUDUUEWCWBWKWRUUTYINUHWRUUTXMXJMJZYIWRUUKUULUUTUVIRUUNUUQXJXMWDSW
      RXMYFXJYHMWRXLYEKLWRYEXLWOWQWPYEXLRBAXEDWSXAEFUUCUUDUUEWEWFWGWBWRXIYGKLAB
      XEDWSXAEFUUCUUDUUEWEWBWKWHWIWKWJWBWLWHWRYMXQUJABCXEDWSXAEFUUCUUDUUEGWMVSW
      OWQWPYNYLRBACXEDWSXAEFUUCUUDUUEGWMWFWN $.
      $( [1-Feb-2007] $)

    $( An inner product times its conjugate. $)
    ipipcj $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( ( A P B ) x. ( B P A ) ) = ( ( abs ` ( A P B ) ) ^ 2 ) ) $=
      ( cnv wcel w3a co cabs cfv c2 cexp ccj cmul cc wceq ipcl absvalsq opreq2d
      syl ipcj eqtr2d ) DHIAEIBEIJZABCKZLMNOKZUGUGPMZQKZUGBACKZQKUFUGRIUHUJSABC
      DEFGTUGUAUCUFUIUKUGQABCDEFGUDUBUE $.
      $( [23-Nov-2007] $)

    $( Orthogonality (meaning inner product is 0) is commutative. $)
    iporthcom $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X )
              -> ( ( A P B ) = 0 <-> ( B P A ) = 0 ) ) $=
      ( cnv wcel co cc0 wceq ccj cfv fveq2 cj0 syl6eq ipcj eqeq1d syl5ib 3com23
      w3a impbid ) DHIZAEIZBEIZUBZABCJZKLZBACJZKLZUIUHMNZKLUGUKUIULKMNZKUHKMOPQ
      UGULUJKABCDEFGRSTUKUJMNZKLUGUIUKUNUMKUJKMOPQUGUNUHKUDUFUEUNUHLBACDEFGRUAS
      TUC $.
      $( [17-Apr-2008] $)
  $}

  ${
    ip0r.1 $e |- X = ( BaseSet ` U ) $.
    ip0r.5 $e |- Z = ( 0vec ` U ) $.
    ip0r.7 $e |- P = ( .i ` U ) $.
    $( Inner product with a zero second argument. $)
    ip0r $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( A P Z ) = 0 ) $=
      ( wcel co cfv c2 cexp cmin ci caddc c4 cc0 wceq cc cnv wa cpv cnm c1 cneg
      cns cmul cdiv nvzcl adantr eqid ipval2 mpd3an3 ax-1cn negcli nvsz opreq2d
      mpan2 fveq2d cr ipval2lem3 recnd subid syl eqtrd ax-icn eqtr4d ipval2lem4
      opreq1d w3a opreq12d mul01i opreq2i 0cn addid1i eqtri syl6eq 4re gt0ne0ii
      recni 4pos div0i ) CUAIZADIZUBZAEBJZAECUCKZJZCUDKZKZLMJZAUEUFZECUGKZJZWHJ
      ZWJKZLMJZNJZOAOEWNJZWHJZWJKZLMJZAOUFZEWNJZWHJZWJKZLMJZNJZUHJZPJZQUIJZRWDW
      EEDIZWGXLSWDXMWECDEFGUJUKZAEBWNCWHWJDFWHULZWNULZWJULZHUMUNWFXLRQUIJRWFXKR
      QUIWFXKRORUHJZPJZRWFWSRXJXRPWFWSWLWLNJZRWFWRWLWLNWFWQWKLMWFWPWIWJWFWOEAWH
      WDWOESZWEWDWMTIYAUEUOUPWMWNCEXPGUQUSUKURUTVJURWFWLTIXTRSWFWLWDWEXMWLVAIXN
      AEBWNCWHWJDFXOXPXQHVBUNVCWLVDVEVFWFXIROUHWFXIXCXCNJZRWFXHXCXCNWFXGXBLMWFX
      FXAWJWFXEWTAWHWDXEWTSWEWDXEEWTWDXDTIXEESOVGUPXDWNCEXPGUQUSWDOTIZWTESVGOWN
      CEXPGUQUSVHUKURUTVJURWFXCTIZYBRSWDWEXMYDXNWDWEXMVKYCYDVGAEOBWNCWHWJDFXOXP
      XQHVIUSUNXCVDVEVFURVLXSRRPJRXRRRPOVGVMVNRVOVPVQVRVJQQVSWAQVSWBVTWCVRVF $.
      $( [1-Nov-2007] $) $( [5-Feb-2007] $)

    $( Inner product with a zero first argument.  Part of proof of Theorem 6.44
       of [Ponnusamy] p. 361. $)
    ip0l $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( Z P A ) = 0 ) $=
      ( cnv wcel wa co ccj cfv cc0 wceq nvzcl adantr ipcj mpd3an3 fveq2d syl6eq
      ip0r cj0 eqtr3d ) CIJZADJZKZAEBLZMNZEABLZOUFUGEDJZUJUKPUFULUGCDEFGQRAEBCD
      FHSTUHUJOMNOUHUIOMABCDEFGHUCUAUDUBUE $.
      $( [2-Nov-2007] $) $( [5-Feb-2007] $)

    $( The inner product of a vector with itself is zero iff the vector is
       zero.  Part of Definition 3.1-1 of [Kreyszig] p. 129. $)
    ipz $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( ( A P A ) = 0 <-> A = Z ) ) $=
      ( cnv wcel wa co cc0 wceq cnm cfv c2 cexp eqid ipidsq cc nvcl recnd sqeq0
      eqeq1d wb syl nvz 3bitrd ) CIJADJKZAABLZMNACOPZPZQRLZMNZUMMNZAENUJUKUNMAB
      CULDFULSZHTUEUJUMUAJUOUPUFUJUMACULDFUQUBUCUMUDUGACULDEFGUQUHUI $.
      $( [24-Jan-2008] $)
  $}

  ${
    $d k x y z J $.  $d k x y z K $.  $d k x y z U $.
    ipcn.p $e |- P = ( .i ` U ) $.
    ipcn.c $e |- C = ( IndMet ` U ) $.
    ipcn.d $e |- D = ( abs o. - ) $.
    ipcn.j $e |- J = ( MetOpen ` C ) $.
    ipcn.k $e |- K = ( MetOpen ` D ) $.
    $( Inner product is jointly continuous in both arguments.  (The proof was
       shortened by Mario Carneiro, 6-May-2014.) $)
    ipcn $p |- ( U e. NrmCVec -> P e. ( ( J tX J ) Cn K ) ) $=
      ( vx vy vz wcel c4 co ccn eqid cc vk cnv cuni c1 cfz cexp cns cfv cpv cnm
      ci cv c2 cmul csu cdiv cmpt2 ctx cba ipfval wceq cdm imsba imsmet uniopn2
      cme syl eqtrd mpt2eq12 syl2anc ctop opntop cnmet ax-mp a1i adantr cnmetba
      fzfid wa cn0 ax-icn cn elfznn adantl nnnn0 expcl sylancr cnmpt2c cnmpt1st
      cnmpt2nd smcn cnmpt22f vacn nmcnc cnmpt21f cmpt sqcn opreq1 cnmpt21 mulcn
      fsum2cn cc0 wne 4nn nncni nnne0i divccn mp2an eqeltrd ) DUBOZCLMEUCZXKUDP
      UEQZUKUAULZUFQZLULZXNMULZDUGUHZQZDUIUHZQZDUJUHZUHZUMUFQZUNQZUAUOZPUPQZUQZ
      EEURQZFRQXJCLMDUSUHZYIYFUQZYGLMCXQDUAXSYAYIYISZXSSZXQSZYASZGUTXJYIXKVAZYO
      YJYGVAXJYIAVBVBZXKADYIYKHVCXJAVFOZYPXKVAADHVDZAEYPYPSJVEVGVHZYSLMYIYIXKXK
      YFVIVJVHXJLMNYENULZPUPQZYFEEFFXKXKTXKSZUUBXJYQEVKOZYRAEJVLVGZUUDFVKOZXJBV
      FOZUUEBIVMZBFKVLVNZVOZUUIXJLMXLYDBUAEFEXKXKUUBIKUUDXJUDPVRUUBUUDXJXMXLOZV
      SZLMXNYCUNEEFFFTXKXKTUUBUUBXJUUCUUJUUDVPZUULUUEUUKUUHVOZUUMUUKLMXNEEFXKXK
      TUUBUUBUULUULUUMUUFTFUCVAUUGBFTBIVQKVEVNZUUKUKTOXMVTOZXNTOWAUUKXMWBOZUUOU
      UJUUPXJXMPWCWDXMWEVGUKXMWFWGWHZUUKLMNYBYTUMUFQZYCEEFFXKXKTUUBUUBUULUULUUM
      UUMUUKLMXTYAEEEFXKXKUUBUUBUULUULUULUUMUUKLMXOXRXSEEEEEXKXKXKXKUUBUUBUULUU
      LUULUULUUKLMEEXKXKUUBUUBUULUULWIUUKLMXNXPXQEEFEEXKXKXKTUUBUUBUULUULUUMUUL
      UUQUUKLMEEXKXKUUBUUBUULUULWJUUNUUBUULXJXQFEURQERQOUUJABXQDEFHJYMIKWKVPWLU
      UBUUBUULXJXSYHERQOUUJADXSEHJYLWMVPWLXJYAEFRQOUUJABDEFYAYNHIJKWNVPWOUUNNTU
      URWPFFRQZOUUKNBFIKWQVOYTYBUMUFWRWSUUNUUNUUMUNFFURQFRQOUUKBFIKWTVOWLXAUUNN
      TUUAWPUUSOZXJPTOPXBXCUUTPXDXEPXDXFNPBFIKXGXHVOYTYEPUPWRWSXI $.
      $( [6-May-2014] $) $( [21-Aug-2007] $)
  $}

