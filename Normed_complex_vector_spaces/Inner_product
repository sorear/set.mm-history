$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                            Inner product
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c .i $.

  $( Extend class notation with the class inner product functions. $)
  cip $a class .i $.

  ${
    $d k n p w x y z $.
    $( Define a function that maps a complex normed vector space to its inner
       product operation in case its norm satisfies the parallelogram identity
       (otherwise the operation is still defined, but not meaningful).  Based
       on Exercise 4(a) of [ReedSimon] p. 63 and Theorem 6.44 of [Ponnusamy]
       p. 361.  Vector addition is ` ( 1st `` w ) ` , the scalar product is
       ` ( 2nd `` w ) ` , and the norm is ` n ` . $)
    df-ip $a |- .i = { <. <. w , n >. , p >. | ( <. w , n >. e. NrmCVec /\
        p = { <. <. x , y >. , z >. | ( ( x e. dom n /\ y e. dom n ) /\
        z = ( sum_ k e. ( 1 ... 4 ) ( ( i ^ k ) x. ( ( n ` ( x ( 1st ` w )
        ( ( i ^ k ) ( 2nd ` w ) y ) ) ) ^ 2 ) ) / 4 ) ) } ) } $.

$(
    df-ip $a |- .i = { <. x , y >. | ( x e. NrmCVec /\ E. g E. s E. n
            ( x = <. <. g , s >. , n >. /\ y = { <. <. a , b >. , c >. |
            ( ( a e. dom n /\ b e. dom n ) /\ c = ( sum_ k e. ( 1 ... 4 )
  ( ( i ^ k ) x. ( ( n ` ( a g ( ( i ^ k ) s b ) ) ) ^ 2 ) ) / 4 ) ) } ) ) } $.

    df-ip $a |- .i = { <. x , y >. | ( x e. NrmCVec /\ E. g E. s E. n
            ( x = <. <. g , s >. , n >. /\ y = { <. <. a , b >. , c >. |
            ( ( a e. dom n /\ b e. dom n ) /\ c =
                ( ( ( ( ( n ` ( a g b ) ) ^ 2 ) +
                ( ( n ` ( a g ( -u 1 s b ) ) ) ^ 2 ) ) +
                ( i x. ( ( ( n ` ( a g ( i s b ) ) ) ^ 2 ) +
                ( ( n ` ( a g ( -u i s b ) ) ) ^ 2 ) ) ) ) / 4 ) ) } ) ) } $.
$)
  $}

  ${
    $d k A $.  $d k B $.  $d k G $.  $d k N $.  $d k S $.
    ipval2lem.1 $e |- M e. V $.
    $( Lemma for ~ ipval2 . $)
    ipval2lem1 $p |- [_ M / k ]_ ( ( i ^ k ) x.
                           ( ( N ` ( A G ( ( i ^ k ) S B ) ) ) ^ 2 ) ) =
              ( ( i ^ M ) x. ( ( N ` ( A G ( ( i ^ M ) S B ) ) ) ^ 2 ) ) $=
      ( ci cv cexp co cfv c2 cmul csb cvv wcel wceq csbopr12g ax-mp csbopr2g
      csbvarg opreq2i eqtr csbopr1g csbfv2g opreq1i fveq2i opreq12i ) DFIDJZKLZ
      AULBCLZELZGMZNKLZOLPZDFULPZDFUPPZOLZIFKLZAVABCLZELZGMZNKLZOLFQRZUQUTSHDFU
      LUPQOTUAURVAUSVEOURIDFUKPZKLZVAVFURVHSHDFIUKQKUBUAVGFIKVFVGFSHDFQUCUAUDUE
      ZUSDFUOPZNKLZVEVFUSVKSHDFUONQKUFUAVJVDNKVJDFUNPZGMZVDVFVJVMSHDFUNQGUGUAVL
      VCGVLADFUMPZELZVCVFVLVOSHDFAUMQEUBUAVNVBAEVNURBCLZVBVFVNVPSHDFULBQCUFUAUR
      VABCVIUHUEUDUEUIUEUHUEUJUE $.
      $( [19-Feb-2007] $) $( [31-Jan-2007] $)
  $}

  ${
    $d k n p w x y z G $.  $d k n p w x y z N $.  $d k n p w x y z S $.
    $d k n p w x y z U $.  $d k x y z A $.  $d k x y z B $.  $d x y z X $.
    ipfval.1 $e |- W = ( 1st ` U ) $.
    ipfval.2 $e |- G = ( 1st ` W ) $.
    ipfval.3 $e |- S = ( 2nd ` W ) $.
    ipfval.4 $e |- N = ( 2nd ` U ) $.
    ipfval.5 $e |- X = ran G $.
    ipfval.6 $e |- P = ( .i ` U ) $.
    $( The inner product function on a normed complex vector space.  The
       definition is meaningful for vector spaces that are also inner product
       spaces, i.e. satisfy the parallelogram law. $)
    ipfval $p |- ( U e. NrmCVec -> P = { <. <. x , y >. , z >. |
        ( ( x e. X /\ y e. X ) /\
       z = ( sum_ k e. ( 1 ... 4 ) ( ( i ^ k ) x. ( ( N ` ( x G
       ( ( i ^ k ) S y ) ) ) ^ 2 ) ) / 4 ) ) } ) $=
      ( vw vn vp cncv wcel c1st cfv c2nd cop cip co cv wa c1 c4 cfz ci cexp c2
      cmul csu cdiv wceq copab2 wrel ncvrel 1st2nd mpan fveq2d syl5eq df-opr
      a1i cdm eleq1d ibi cvv fvex w3a eqeltr dmexg ax-mp eqid oprabex2 visset
      opth wb id syl6eqr dmeqd eleq2d anbi12d adantl fveq1d fveq2 syl opreqd
      opreq2d eqtrd sylan9eqr opreq1d sumeq2sdv eqeq2d oprabbidv sylbi df-ip
      oprabval6g mpan2 mp3an12 cr wf ncvf fdm anbi1d 3eqtr2d ) FUAUBZDFUCUDZFUE
      UDZUFZUGUDZXMXNUGUHZAUIZKUBZBUIZKUBZUJZCUIZUKULUMUHZUNGUIUOUHZXRYEXTEUHZH
      UHZIUDZUPUOUHZUQUHZGURZULUSUHZUTZUJZABCVAZXLFUGUDXPDXLFXOUGUAVBXLFXOUTVCF
      UAVDVEZVFQVGXQXPUTXLXMXNUGVHVIXLXQXRIVJZUBZXTYQUBZUJZYMUJZABCVAZYOXLXOUAU
      BZXQUUBUTZXLUUCXLFXOUAYPVKVLXMVMUBZXNVMUBZUUCUUDFUCVNFUEVNZUUEUUFUUCVOUUB
      VMUBUUDABCYQYQYLUUBIVMUBYQVMUBIXNVMOUUGVPIVMVQVRZUUHUUBVSVTRSTXMXNUAXRSUI
      ZVJZUBZXTUUJUBZUJZYCYDYEXRYEXTRUIZUEUDZUHZUUNUCUDZUHZUUIUDZUPUOUHZUQUHZGU
      RZULUSUHZUTZUJZABCVAZUUBUGVMVMVMUUNUUIUFXOUTUUNXMUTZUUIXNUTZUJZUVFUUBUTUU
      NUUIXMXNRWASWAUUGWBUVIUVEUUAABCUVIUUMYTUVDYMUVHUUMYTWCUVGUVHUUKYRUULYSUVH
      UUJYQXRUVHUUIIUVHUUIXNIUVHWDOWEZWFZWGUVHUUJYQXTUVKWGWHWIUVIUVCYLYCUVIUVBY
      KULUSUVIYDUVAYJGUVIUUTYIYEUQUVIUUSYHUPUOUVHUVGUUSUURIUDYHUVHUURUUIIUVJWJU
      VGUURYGIUVGUURXRUUPHUHYGUVGUUQHXRUUPUVGUUNJUTZUUQHUTUVGUUNXMJUVGWDLWEZUVL
      UUQJUCUDHUUNJUCWKMWEWLWMUVGUUPYFXRHUVGUUOEYEXTUVGUUOJUEUDEUVGUUNJUEUVMVFN
      WEWMWNWOVFWPWQWNWRWQWSWHWTXAABCRGSTXBXCXDXEWLXLUUAYNABCXLYTYBYMXLYRXSYSYA
      XLYQKXRXLKXFIXGYQKUTFHIJKLMOPXHKXFIXIWLZWGXLYQKXTUVNWGWHXJWTWOXK $.
      $( [17-Apr-2007] $) $( [10-Apr-2007] $)

    $( Value of the inner product on a normed complex vector space.  The
       definition is meaningful for vector spaces that are also inner product
       spaces, i.e. satisfy the parallelogram law.  The vector space is
       ` W ` , the vector (group) addition operation is ` G ` , the scalar
       product is ` S ` , the norm is ` N ` , and the set of vectors is
       ` X ` .  Equation 6.45 of [Ponnusamy] p. 361. $)
    ipval $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B ) =
                ( sum_ k e. ( 1 ... 4 ) ( ( i ^ k ) x. ( ( N ` ( A G
                    ( ( i ^ k ) S B ) ) ) ^ 2 ) ) / 4 ) ) $=
      ( vx vy vz cncv wcel w3a co cv wa c1 c4 cfz ci cexp cfv c2 cmul csu cdiv
      wceq copab2 ipfval opreqd 3ad2ant1 oprex opreq1 fveq2d opreq1d opreq2d
      sumeq2sdv opreq2 eqid oprabval2 3adant1 eqtrd ) ETUAZAJUAZBJUAZUBABCUCZAB
      QUDZJUARUDZJUAUESUDUFUGUHUCZUIFUDUJUCZVPVSVQDUCZGUCZHUKZULUJUCZUMUCZFUNZU
      GUOUCZUPUEQRSUQZUCZVRVSAVSBDUCZGUCZHUKZULUJUCZUMUCZFUNZUGUOUCZVLVMVOWHUPV
      NVLCWGABQRSCDEFGHIJKLMNOPURUSUTVMVNWHWOUPVLQRSABJJWFWOWGVRVSAVTGUCZHUKZUL
      UJUCZUMUCZFUNZUGUOUCWNUGUOVAVPAUPZWEWTUGUOXAVRWDWSFXAWCWRVSUMXAWBWQULUJXA
      WAWPHVPAVTGVBVCVDVEVFVDVQBUPZWTWNUGUOXBVRWSWMFXBWRWLVSUMXBWQWKULUJXBWPWJH
      XBVTWIAGVQBVSDVGVEVCVDVEVFVDWGVHVIVJVK $.
      $( [19-Apr-2007] $) $( [31-Jan-2007] $)

    $( Lemma for ~ ipval2 . $)
    ipval2lem2 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
               ( ( N ` ( A G ( C S B ) ) ) ^ 2 ) e. RR ) $=
      ( cncv wcel w3a cc wa co cfv cr c2 cexp ncvcl 3simp1 adantr ncvgclOLDOLD
      3simp2 ncvsclOLDOLD 3com23 3expa 3adantl2 syl3anc sylanc resqclt syl ) FQ
      RZAJRZBJRZSZCTRZUAZACBEUBZGUBZHUCZUDRZVHUEUFUBUDRUTVGJRZVIVEVGFGHIJKLNOUG
      VCUTVDUTVAVBUHUIZUTVAVFJRZVJVEAVFFGIJKLOUJVKVCVAVDUTVAVBUKUIUTVBVDVLVAUTV
      BVDVLUTVDVBVLCBEFGIJKLMOULUMUNUOUPUQVHURUS $.
      $( [19-Apr-2007] $) $( [1-Feb-2007] $)

    $( Lemma for ~ ipval2 . $)
    ipval2lem3 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( ( N ` ( A G B ) ) ^ 2 ) e. RR ) $=
      ( cncv wcel w3a co cfv c2 cexp c1 cr wceq wa cvc vcid ncvv sylan opreq2d
      fveq2d opreq1d 3adant2 eqcomd cc ax1cn ipval2lem2 mpan2 eqeltrd ) EPQZAIQ
      ZBIQZRZABFSZGTZUAUBSZAUCBDSZFSZGTZUAUBSZUDVDVKVGVAVCVKVGUEVBVAVCUFZVJVFUA
      UBVLVIVEGVLVHBAFHUGQVCVHBUEVABDFHIKLNUHEHJUIUJUKULUMUNUOVDUCUPQVKUDQUQABU
      CCDEFGHIJKLMNOURUSUT $.
      $( [20-Apr-2007] $) $( [1-Feb-2007] $)

    $( Lemma for ~ ipval2 . $)
    ipval2lem4 $p |- ( ( ( U e. NrmCVec /\ A e. X /\ B e. X ) /\ C e. CC ) ->
               ( ( N ` ( A G ( C S B ) ) ) ^ 2 ) e. CC ) $=
      ( cncv wcel w3a cc wa co cfv c2 cexp ipval2lem2 recnd ) FQRAJRBJRSCTRUAAC
      BEUBGUBHUCUDUEUBABCDEFGHIJKLMNOPUFUG $.
      $( [20-Apr-2007] $) $( [1-Feb-2007] $)

    $( Expansion of the inner product value ~ ipval . _Warning:  The HTML proof
       page is 0.5MB in size_. $)
    ipval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A P B )
               = ( ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) +
               ( i x. ( ( ( N ` ( A G ( i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u i S B ) ) ) ^ 2 ) ) ) ) / 4 ) ) $=
      ( vk cncv wcel w3a co c1 c4 cfz ci cv cexp cfv c2 cmul csu cdiv cneg
      cmin caddc ipval cc wceq negsubt subclt axicn ipval2lem4 mpan2 axmulcl
      mpan syl ax1cn negcl sylanc mulm1t opreq2d eqtrd mulneg1t opreq12d
      subdit mp3an1 opreq1d sub23t syl3anc 3eqtr4d wa i4 a1i cvc vcid ncvv
      sylan opreq1i syl5eq fveq2d 3adant2 ipval2lem3 recnd mulid2t c3 csb df-4
      3re recn addcom eqtr opreq2i sumeq1i cz 1z oprex fsum4 ax-mp elisseti
      ipval2lem1 exp1t fveq2i opreq12i 2cn df-2 csbeq1 i2 3eqtr3 cr df-3 i3
      4re eqtr3 3eqtr axaddcom subadd23t eqtr4d ) EQRZAIRZBIRZSZABCTUAUBUCTZUDP
      UEUFTZAYLBDTFTGUGUHUFTZUITZPUJZUBUKTABFTZGUGZUHUFTZAUAULZBDTZFTZGUGZUHUFT
      ZUMTZUDAUDBDTZFTZGUGZUHUFTZAUDULZBDTZFTZGUGZUHUFTZUMTZUITZUNTZUBUKTABCDEP
      FGHIJKLMNOUOYJYOUUPUBUKYJUDUUHUITZYSUUCUITZUNTZUUIUUMUITZUNTZUDUBUFTZAUVB
      BDTZFTZGUGZUHUFTZUITZUNTZUUOUUCUMTZYRUNTZYOUUPYJUVAUVIUVGYRUNYJUUQUUCUMTZ
      UDUUMUITZULZUNTZUVKUVLUMTZUVAUVIUVKUPRZUVLUPRZUVNUVOUQYJUVKUVLURUUQUPRZUU
      CUPRZUVPYJUUQUUCUSYJUUHUPRZUVRYJUDUPRZUVTUTABUDCDEFGHIJKLMNOVAVBZUWAUVTUV
      RUTUDUUHVCVDVEZYJYSUPRUVSUAVFVGABYSCDEFGHIJKLMNOVAVBZVHYJUUMUPRZUVQYJUUIU
      PRUWEUDUTVGABUUICDEFGHIJKLMNOVAVBZUWAUWEUVQUTUDUUMVCVDVEZVHYJUUSUVKUUTUVM
      UNYJUUSUUQUUCULZUNTZUVKYJUURUWHUUQUNYJUVSUURUWHUQUWDUUCVIVEVJUVRUVSUWIUVK
      UQYJUUQUUCURUWCUWDVHVKYJUWEUUTUVMUQZUWFUWAUWEUWJUTUDUUMVLVDVEVMYJUVIUUQUV
      LUMTZUUCUMTZUVOYJUUOUWKUUCUMUVTUWEUUOUWKUQZYJUWAUVTUWEUWMUTUDUUHUUMVNVOUW
      BUWFVHVPUVRUVQUVSUWLUVOUQYJUUQUVLUUCVQUWCUWGUWDVRVKVSYJUVGUAYRUITZYRYGYIU
      VGUWNUQYHYGYIVTZUVBUAUVFYRUIUVBUAUQUWOWAWBUWOUVEYQUHUFUWOUVDYPGUWOUVCBAFU
      WOUABDTZBUVCHWCRYIUWPBUQYGBDFHIKLNWDEHJWEWFUVBUABDWAWGWHVJWIVPVMWJYJYRUPR
      ZUWNYRUQYJYRABCDEFGHIJKLMNOWKWLZYRWMVEVKVMYOUVHUQYJYOUAUAWNUNTZUCTZYNPUJZ
      PUAYNWOZPUAUAUNTZYNWOZUNTZPUAUHUNTZYNWOZUNTZPUWSYNWOZUNTZUVHYKUWTYNPUBUWS
      UAUCUBWNUAUNTUWSWPWNUAWNWQWRVFWSWTZXAXBUAXCRUXAUXJUQXDYNPUAYLYMUIXEXFXGUX
      HUVAUXIUVGUNUXEUUSUXGUUTUNUXBUUQUXDUURUNUXBUDUAUFTZAUXLBDTZFTZGUGZUHUFTZU
      ITUUQABDPFUAGUAXCXDXHXIUXLUDUXPUUHUIUWAUXLUDUQUTUDXJXGZUXOUUGUHUFUXNUUFGU
      XMUUEAFUXLUDBDUXQWGXAXKWGXLWTPUHYNWOZUDUHUFTZAUXSBDTZFTZGUGZUHUFTZUITUXDU
      URABDPFUHGUHUPXMXHXIUHUXCUQUXRUXDUQXNPUHUXCYNXOXGUXSYSUYCUUCUIXPUYBUUBUHU
      FUYAUUAGUXTYTAFUXSYSBDXPWGXAXKWGXLXQXLPWNYNWOZUDWNUFTZAUYEBDTZFTZGUGZUHUF
      TZUITUXGUUTABDPFWNGWNXRWQXHXIWNUXFUQUYDUXGUQWNUHUAUNTUXFXSUHUAXMVFWSWTPWN
      UXFYNXOXGUYEUUIUYIUUMUIXTUYHUULUHUFUYGUUKGUYFUUJAFUYEUUIBDXTWGXAXKWGXLXQX
      LPUBYNWOZUXIUVGUBUWSUQUYJUXIUQUXKPUBUWSYNXOXGABDPFUBGUBXRYAXHXIYBXLYCWBYJ
      UUPUUOUUDUNTZUVJUUDUPRZUUOUPRZUUPUYKUQYJUUDUUOYDUWQUVSUYLYJYRUUCUSUWRUWDV
      HYJUUNUPRZUYMUVTUWEUYNYJUUHUUMUSUWBUWFVHUWAUYNUYMUTUDUUNVCVDVEZVHUYMUVSUW
      QUVJUYKUQYJUUOUUCYRYEUYOUWDUWRVRYFVSVPVK $.
      $( [20-Apr-2007] $) $( [31-Jan-2007] $)

    $( Four times the inner product value ~ ipval2 , useful for simplifying
       certain proofs. $)
    4ipval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
        ( 4 x. ( A P B ) ) = ( ( ( ( N ` ( A G B ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) +
               ( i x. ( ( ( N ` ( A G ( i S B ) ) ) ^ 2 ) -
               ( ( N ` ( A G ( -u i S B ) ) ) ^ 2 ) ) ) ) ) $=
      ( cncv wcel w3a c4 co cmul cfv c2 cexp c1 cneg cmin ci caddc cdiv ipval2
      opreq2d cc wceq axaddcl subclt cr ncvcl 3simp1 ncvgclOLDOLD sylanc recnd
      sqclt syl 3simp2 ax1cn negcl ncvsclOLDOLD mp3an2 3adant2 syl3anc axicn
      axmulcl mpan cc0 wne 4re recn 4pos gt0ne0i divcan2t mp3an13 eqtrd ) EPQZA
      IQZBIQZRZSABCTZUATSABFTZGUBZUCUDTZAUEUFZBDTZFTZGUBZUCUDTZUGTZUHAUHBDTZFTZ
      GUBZUCUDTZAUHUFZBDTZFTZGUBZUCUDTZUGTZUATZUITZSUJTZUATZXIWGWHXJSUAABCDEFGH
      IJKLMNOUKULWGXIUMQZXKXIUNZWQUMQZXHUMQZXLWGWQXHUOWKUMQZWPUMQZXNWGWKWPUPWGW
      JUMQXPWGWJWDWIIQWJUQQWGWIEFGHIJKMNURWDWEWFUSZABEFHIJKNUTVAVBWJVCVDWGWOUMQ
      XQWGWOWDWNIQZWOUQQWGWNEFGHIJKMNURXRWDWEWMIQZXSWGAWMEFHIJKNUTXRWDWEWFVEZWD
      WFXTWEWDWLUMQWFXTUEVFVGWLBDEFHIJKLNVHVIVJVKVAVBWOVCVDVAWGXGUMQZXOXAUMQZXF
      UMQZYBWGXAXFUPWGWTUMQYCWGWTWDWSIQZWTUQQWGWSEFGHIJKMNURXRWDWEWRIQZYEWGAWRE
      FHIJKNUTXRYAWDWFYFWEWDUHUMQZWFYFVLUHBDEFHIJKLNVHVIVJVKVAVBWTVCVDWGXEUMQYD
      WGXEWDXDIQZXEUQQWGXDEFGHIJKMNURXRWDWEXCIQZYHWGAXCEFHIJKNUTXRYAWDWFYIWEWDX
      BUMQWFYIUHVLVGXBBDEFHIJKLNVHVIVJVKVAVBXEVCVDVAYGYBXOVLUHXGVMVNVDVASUMQXLS
      VOVPXMSVQVRSVQVSVTSXIWAWBVDWC $.
      $( [?] $) $( [10-Apr-2007] $)
  $}

  ${
    $d k n p w x y z G $.  $d k n p w x y z N $.  $d k n p w x y z S $.
    $d k n p w x y z U $.  $d k x y z A $.  $d k x y z B $.  $d x y z X $.
    nmsqip.1 $e |- W = ( 1st ` U ) $.
    nmsqip.2 $e |- G = ( 1st ` W ) $.
    nmsqip.4 $e |- N = ( 2nd ` U ) $.
    nmsqip.5 $e |- X = ran G $.
    nmsqip.6 $e |- P = ( .i ` U ) $.
    $( Norm squared expressed in terms of inner product.  Part of proof of
       Theorem 6.44 of [Ponnusamy] p. 361. $)
    nmsqip $p |- ( ( U e. NrmCVec /\ A e. X ) ->
                ( ( N ` A ) ^ 2 ) = ( A P A ) ) $=
      ( cncv wcel wa co cfv c2 cexp c1 cneg c2nd cmin ci cmul caddc c4 cdiv
      wceq eqid ipval2 3anidm23 cc0 cabs cvc vc2 ncvv sylan fveq2d cc 2cn ncvs
      mp3an2 cle wbr 0re 2re 2pos ltlei absid ax-mp opreq1i a1i 3eqtrd opreq1d
      ncvcl recnd cn0 2nn0 mulexpt mp3an13 syl sq2 cgi vcrinv ncvz0 adantr
      eqtrd sq0 syl6eq opreq12d sqclt 4re recn axmulcl mpan subid1t csqr cr
      1re renegcl absreimt mp2an axicn ax1cn mulneg2 mulid1 negeqi eqtr
      opreq2i fveq2i sqnegt 3eqtr3 3eqtr2 negcl addcl 3eqtr4a vcdir mp3anr1
      mpanr1 vcid 3eqtr3d opreq2d w3a ipval2lem4 mpan2 subidt mul01 ax0id
      eqtr2d wne 4pos gt0ne0i divcan3t 3eqtr2rd ) CMNZAGNZOZAABPZAADPZEQZRSPZAT
      UAZAFUBQZPDPZEQZRSPZUCPZUDAUDAUUDPZDPZEQZRSPZAUDUAZAUUDPZDPZEQZRSPZUCPZUE
      PZUFPZUGUHPZUGAEQZRSPZUEPZUGUHPZUVCYPYQYSUVAUIAABUUDCDEFGHIUUDUJZJKLUKULY
      RUVDUUTUGUHYRUUTUVDUMUFPZUVDYRUUHUVDUUSUMUFYRUUHUVDUMUCPZUVDYRUUBUVDUUGUM
      UCYRUUBRUVBUEPZRSPZRRSPZUVCUEPZUVDYRUUAUVIRSYRUUARAUUDPZEQZRUNQZUVBUEPZUV
      IYRYTUVMEFUONZYQYTUVMUIYPAUUDDFGIUVFKUPCFHUQZURUSYPRUTNZYQUVNUVPUIVARAUUD
      CDEFGHIUVFJKVBVCUVPUVIUIYRUVORUVBUEUMRVDVEUVORUIUMRVFVGVHVIRVGVJVKVLVMVNV
      OYRUVBUTNZUVJUVLUIZYRUVBACDEFGHIJKVPVQZUVSUVTRVRNUWAVAVSRUVBRVTWAWBUVLUVD
      UIYRUVKUGUVCUEWCVLVMVNYRUUGUMRSPUMYRUUFUMRSYRUUFDWDQZEQZUMYRUUEUWCEUVQYQU
      UEUWCUIYPAUUDDFGUWCIUVFKUWCUJZWEUVRURUSYPUWDUMUIYQCDEFUWCHIJUWEWFWGWHVOWI
      WJWKYRUVDUTNZUVHUVDUIYRUVCUTNZUWFYRUVTUWGUWBUVBWLWBZUGUTNZUWGUWFUGWMWNZUG
      UVCWOWPWBZUVDWQWBWHYRUUSUDUMUEPUMYRUURUMUDUEYRUURUULUULUCPZUMYRUUQUULUULU
      CYRUUPUUKRSYRTUUMUFPZAUUDPZEQZTUDUFPZAUUDPZEQZUUPUUKYRUWMUNQZUVBUEPZUWPUN
      QZUVBUEPZUWOUWRUWSUXAUVBUEUWSTRSPZUXCUFPZWRQZTUDTUEPZUFPZUNQZUXATUDUUCUEP
      ZUFPZUNQZUXCUUCRSPZUFPZWRQZUWSUXETWSNZUUCWSNUXKUXNUIWTTWTXATUUCXBXCUXJUWM
      UNUXIUUMTUFUXIUXFUAUUMUDTXDXEXFUXFUDUDXDXGZXHXIXJXKUXMUXDWRUXLUXCUXCUFTUT
      NZUXLUXCUIXETXLVKXJXKXMUXOUXOUXHUXEUIWTWTTTXBXCUXGUWPUNUXFUDTUFUXPXJXKXNV
      LYPUWMUTNYQUWOUWTUITUUMXEUDXDXOZXPUWMAUUDCDEFGHIUVFJKVBVCYPUWPUTNYQUWRUXB
      UITUDXEXDXPUWPAUUDCDEFGHIUVFJKVBVCXQYRUWNUUOEUVQYQUWNUUOUIYPUVQYQOZUWNTAU
      UDPZUUNDPZUUOUVQUUMUTNZYQUWNUYAUIZUXRUVQUXQUYBYQUYCXETUUMAUUDDFGIUVFKXRXS
      XTUXSUXTAUUNDAUUDDFGIUVFKYAZVOWHUVRURUSYRUWQUUJEUVQYQUWQUUJUIYPUXSUWQUXTU
      UIDPZUUJUVQUDUTNZYQUWQUYEUIZXDUVQUXQUYFYQUYGXETUDAUUDDFGIUVFKXRXSXTUXSUXT
      AUUIDUYDVOWHUVRURUSYBVOYCYRUULUTNZUWLUMUIYPYQUYHYPYQYQYDUYFUYHXDAAUDBUUDC
      DEFGHIUVFJKLYEYFULUULYGWBWHYCUDXDYHWJWKYRUWFUVGUVDUIUWKUVDYIWBYJVOYRUWGUV
      EUVCUIZUWHUWIUWGUGUMYKUYIUWJUGWMYLYMUGUVCYNWAWBYO $.
      $( [?] $) $( [1-Feb-2007] $)
  $}

  ${
    $d k A $.  $d k B $.  $d k G $.  $d k U $.  $d k W $.  $d k X $.
    ipcl.1 $e |- W = ( 1st ` U ) $.
    ipcl.2 $e |- G = ( 1st ` W ) $.
    ipcl.5 $e |- X = ran G $.
    ipcl.6 $e |- P = ( .i ` U ) $.
    $( An inner product is a complex number. $)
    ipcl $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( A P B ) e. CC ) $=
      ( vk cncv wcel w3a co c1 c4 cfz ci cv cexp c2nd cfv c2 cmul csu cdiv cc
      eqid ipval wral wa axmulcl cn cn0 elfznnt nnnn0t axicn expclt mpan 3syl
      adantl ipval2lem4 sylan2 sylanc r19.21aiva cuz cz cle wbr 1z eluz1 4nn
      nnzt ax-mp cmin clt cc0 ax1cn subid 4pos eqbrtr wb 1nn nnlem1ltt mp2an
      mpbir mpbir2an fsumclt wne 4re recn gt0ne0i divclt mp3an23 eqeltrd ) DMNA
      GNBGNOZABCPQRSPZTLUAZUBPZAXABFUCUDZPEPDUCUDZUDUEUBPZUFPZLUGZRUHPZUIABCXBD
      LEXCFGHIXBUJZXCUJZJKUKWRXEUINZLWSULZXFUINZXGUINZWRXJLWSXAUINZXDUINZXJWRWT
      WSNZUMXAXDUNXPXNWRXPWTUONWTUPNZXNWTRUQWTURTUINXQXNUSTWTUTVAVBZVCWRXNXOXPA
      BXACXBDEXCFGHIXHXIJKVDXRVEVFVGRQVHUDNZXKXLXSRVINZQRVJVKZQRVLVMRUONZXTVNRV
      OVPYAQQVQPZRVRVKZYCVSRVRQVTWAWBWCQUONYBYAYDWDWEVNQRWFWGWHWIXELQRWJVAXLRUI
      NRVSWKXMRWLWMRWLWBWNXFRWOWPVBWQ $.
      $( [?] $) $( [1-Feb-2007] $)

    $( The complex conjugate of an inner product reverses its arguments.
       Part of proof of Theorem 6.44 of [Ponnusamy] p. 361. $)
    ipcj $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( * ` ( A P B ) ) = ( B P A ) ) $=
      ( cncv wcel w3a co c2nd cfv c2 cexp c1 cneg cmin ci cmul caddc c4 cdiv
      ccj cc wceq axaddcl subclt eqid ipval2lem3 recnd ax1cn negcl ipval2lem4
      mpan2 sylanc axicn axmulcl mpan syl cc0 wne 4re recn 4pos gt0ne0i cjdivt
      mp3an23 cr cjreimt resubclt ipval2lem2 submul2t mp3an2 cvc vccom ncvv
      syl3an1 fveq2d opreq1d ncvdif opreq12d negsubdi2t ncvpi 3com23 eqcomd
      eqtrd opreq2d 3eqtrd cjret ax-mp opreq2i syl5eq ipval2 3eqtr4d ) DLMZAGMZ
      BGMZNZABEOZDPQZQZRSOZATUAZBFPQZOEOXEQZRSOZUBOZUCAUCBXIOEOXEQZRSOZAUCUAZBX
      IOEOXEQZRSOZUBOZUDOZUEOZUFUGOZUHQZBAEOZXEQZRSOZBXHAXIOEOXEQZRSOZUBOZUCBUC
      AXIOEOXEQZRSOZBXOAXIOEOXEQZRSOZUBOZUDOZUEOZUFUGOZABCOZUHQBACOZXCYBXTUHQZU
      FUHQZUGOZYPXCXTUIMZYBUUAUJZXLUIMZXSUIMZUUBXCXLXSUKXGUIMXKUIMZUUDXCXGXKULX
      CXGABCXIDEXEFGHIXIUMZXEUMZJKUNZUOXCXHUIMZUUFTUPUQZABXHCXIDEXEFGHIUUGUUHJK
      URUSUTZXCXRUIMZUUEXNUIMZXQUIMZUUMXCXNXQULXCUCUIMZUUNVAABUCCXIDEXEFGHIUUGU
      UHJKURUSZXCXOUIMZUUOUCVAUQZABXOCXIDEXEFGHIUUGUUHJKURUSZUTZUUPUUMUUEVAUCXR
      VBVCVDUTUUBUFUIMUFVEVFUUCUFVGVHUFVGVIVJXTUFVKVLVDXCYSUFUGOYPUUAXCYSYOUFUG
      XCYSXLXSUBOZXLUCXRUAZUDOZUEOZYOXLVMMZXRVMMZYSUVBUJXCXLXRVNXGVMMXKVMMZUVFX
      CXGXKVOUUIXCUUJUVHUUKABXHCXIDEXEFGHIUUGUUHJKVPUSUTXNVMMZXQVMMZUVGXCXNXQVO
      XCUUPUVIVAABUCCXIDEXEFGHIUUGUUHJKVPUSXCUURUVJUUSABXOCXIDEXEFGHIUUGUUHJKVP
      USUTUTUUDUUMUVBUVEUJZXCUUDUUPUUMUVKVAXLUCXRVQVRUULUVAUTXCXLYHUVDYNUEXCXGY
      EXKYGUBXCXFYDRSXCXDYCXEFVSMXAXBXDYCUJWTABEFGIJVTDFHWAWBWCWDXCXJYFRSABXIDE
      XEFGHIUUGUUHJWEWDWFXCUVCYMUCUDXCUVCXQXNUBOZYMUUNUUOUVCUVLUJXCXNXQWGUUQUUT
      UTXCXQYJXNYLUBXCXPYIRSXCYIXPWTXBXAYIXPUJBAXIDEXEFGHIUUGUUHJWHWIWJWDXCXMYK
      RSABXIDEXEFGHIUUGUUHJWHWDWFWKWLWFWMWDYTUFYSUGUFVMMYTUFUJVGUFWNWOWPWQWKXCY
      QYAUHABCXIDEXEFGHIUUGUUHJKWRWCWTXBXAYRYPUJBACXIDEXEFGHIUUGUUHJKWRWIWS $.
      $( [?] $) $( [1-Feb-2007] $)
  $}

  ${
    ip0r.1 $e |- W = ( 1st ` U ) $.
    ip0r.2 $e |- G = ( 1st ` W ) $.
    ip0r.5 $e |- X = ran G $.
    ip0r.6 $e |- Z = ( Id ` G ) $.
    ip0r.7 $e |- P = ( .i ` U ) $.
    $( Inner product with a zero second argument. $)
    ip0r $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( A P Z ) = 0 ) $=
      ( cncv wcel wa co c2nd cfv c2 cexp c1 cneg cmin ci cmul caddc c4 cdiv
      cc0 wceq eqid ipval2 pm3.26 pm3.27 cvc ncvv vc0cl syl adantr syl3anc
      vcinv0 opreq2d fveq2d opreq1d cc cr ipval2lem3 recnd subidt eqtrd axicn
      negcl vcz mpan2 eqtr4d w3a ipval2lem4 opreq12d mul01 opreq2i 0cn addid1
      eqtr syl6eq 4re recn 4pos gt0ne0i div0 ) CMNZAFNZOZAGBPZAGDPZCQRZRZSTPZAU
      AUBGEQRZPZDPZWORZSTPZUCPZUDAUDGWRPZDPZWORZSTPZAUDUBZGWRPZDPZWORZSTPZUCPZU
      EPZUFPZUGUHPZUIWJWKGFNZWMXPUJWLAGBWRCDWOEFHIWRUKZWOUKZJLULWJWKUMZWJWKUNZW
      JXQWKWJEUONZXQCEHUPZDEFGIJKUQURUSZUTWLXPUIUGUHPUIWLXOUIUGUHWLXOUIUDUIUEPZ
      UFPZUIWLXCUIXNYEUFWLXCWQWQUCPZUIWLXBWQWQUCWLXAWPSTWLWTWNWOWLWSGADWJWSGUJZ
      WKWJYBYHYCWRDEGIXRKVAURUSVBVCVDVBWLWQVENYGUIUJWLWQWJWKXQWQVFNWLAGBWRCDWOE
      FHIXRXSJLVGXTYAYDUTVHWQVIURVJWLXMUIUDUEWLXMXGXGUCPZUIWLXLXGXGUCWLXKXFSTWL
      XJXEWOWLXIXDADWJXIXDUJWKWJXIGXDWJYBXIGUJZYCYBXHVENYJUDVKVLXHWRDEFGIXRJKVM
      VNURWJYBXDGUJZYCYBUDVENZYKVKUDWRDEFGIXRJKVMVNURVOUSVBVCVDVBWLXGVENZYIUIUJ
      WJWKXQYMWLWJWKXQVPYLYMVKAGUDBWRCDWOEFHIXRXSJLVQVNXTYAYDUTXGVIURVJVBVRYFUI
      UIUFPUIYEUIUIUFUDVKVSVTUIWAWBWCWDVDUGUGWEWFUGWEWGWHWIWDVJ $.
      $( [?] $) $( [5-Feb-2007] $)

    $( Inner product with a zero first argument.  Part of proof of Theorem 6.44
       of [Ponnusamy] p. 361. $)
    ip0l $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( Z P A ) = 0 ) $=
      ( cncv wcel wa co ccj cfv cc0 wceq ipcj pm3.26 pm3.27 cvc ncvv vc0cl syl
      adantr syl3anc ip0r fveq2d cj0 syl6eq eqtr3d ) CMNZAFNZOZAGBPZQRZGABPZSUO
      UPGFNZUSUTTUQAGBCDEFHIJLUAUOUPUBUOUPUCUOVAUPUOEUDNVACEHUEDEFGIJKUFUGUHUIU
      QUSSQRSUQURSQABCDEFGHIJKLUJUKULUMUN $.
      $( [?] $) $( [5-Feb-2007] $)
  $}

