$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Induced metric of a normed complex vector space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d d u M $.  $d d u N $.  $d d u U $.
    imsval.3 $e |- M = ( -v ` U ) $.
    imsval.6 $e |- N = ( norm ` U ) $.
    imsval.8 $e |- D = ( IndMet ` U ) $.
    $( Value of the induced metric of a normed complex vector space. $)
    imsval $p |- ( U e. NrmCVec -> D = ( N o. M ) ) $=
      ( vu vd cnv wcel cims cfv ccom cv cnm cnsb wceq fveq2 syl6eqr coeq1d
      coeq2d eqtrd df-ims cvv fvex eqeltr coex fvopab4 syl5eq ) BJKBLMDCNZAHIBH
      OZPMZULQMZNZUKJLULBRZUODUNNUKUPUMDUNUPUMBPMZDULBPSFTUAUPUNCDUPUNBQMZCULBQ
      SETUBUCHIUDDCDUQUEFBPUFUGCURUEEBQUFUGUHUIGUJ $.
      $( [12-Sep-2007] $) $( [11-Sep-2007] $)
  $}

  ${
    imsdval.1 $e |- X = ( Base ` U ) $.
    imsdval.3 $e |- M = ( -v ` U ) $.
    imsdval.6 $e |- N = ( norm ` U ) $.
    imsdval.8 $e |- D = ( IndMet ` U ) $.
    $( Value of the induced metric (distance function) of a normed complex
       vector space.  Equation 1 of [Kreyszig] p. 59. $)
    imsdval $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                  ( A D B ) = ( N ` ( A M B ) ) ) $=
      ( cnv wcel w3a cop cfv co ccom wceq imsval 3ad2ant1 fveq1d wfun cdm fvco
      cr wf nvf ffun syl cxp nvmf opelxpi 3adant1 fdm eleqtrrd syl3anc eqtrd
      df-opr fveq2i 3eqtr4g ) DLMZAGMZBGMZNZABOZCPZVFEPZFPZABCQABEQZFPVEVGVFFER
      ZPZVIVEVFCVKVBVCCVKSVDCDEFIJKTUAUBFUCZEUCZVFEUDZMVLVISVEVFFEUEVBVCVMVDVBG
      UFFUGVMDFGHJUHGUFFUIUJUAVBVCVNVDVBGGUKZGEUGZVNDEGHIULZVPGEUIUJUAVEVFVPVOV
      CVDVFVPMVBABGGUMUNVBVCVOVPSZVDVBVQVSVRVPGEUOUJUAUPUQURABCUSVJVHFABEUSUTVA
      $.
      $( [12-Sep-2007] $) $( [11-Sep-2007] $)
  $}

  ${
    imsdval2.1 $e |- X = ( Base ` U ) $.
    imsdval2.2 $e |- G = ( +v ` U ) $.
    imsdval2.4 $e |- S = ( .s ` U ) $.
    imsdval2.6 $e |- N = ( norm ` U ) $.
    imsdval2.8 $e |- D = ( IndMet ` U ) $.
    $( MAY BE REVISED $)
    $( Value of the distance function of the induced metric of a normed
       complex vector space.  Equation 1 of [Kreyszig] p. 59. $)
    imsdval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                  ( A D B ) = ( N ` ( A G ( -u 1 S B ) ) ) ) $=
      ( cnv wcel w3a co cnsb cfv c1 cneg eqid imsdval nvmval fveq2d eqtrd ) ENO
      AHOBHOPZABCQABERSZQZGSATUABDQFQZGSABCEUHGHIUHUBZLMUCUGUIUJGABDEFUHHIJKUKU
      DUEUF $.
      $( [11-Sep-2007] $) $( [28-Nov-2006] $)
  $}

  ${
    nvnd.1 $e |- X = ( Base ` U ) $.
    nvnd.5 $e |- Z = ( 0v ` U ) $.
    nvnd.6 $e |- N = ( norm ` U ) $.
    nvnd.8 $e |- D = ( IndMet ` U ) $.
    $( The norm of a normed complex vector space expressed in terms of the
       distance function of its induced metric.  Problem 1 of [Kreyszig]
       p. 63. $)
    nvnd $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( N ` A ) = ( A D Z ) ) $=
      ( cnv wcel wa co cnsb cfv wceq nvzcl adantr eqid imsdval mpd3an3 c1 cneg
      cns cpv nvmval cc ax1cn negcl nvsz mpan2 opreq2d nv0rid 3eqtrd fveq2d
      eqtr2d ) CKLZAELZMZAFBNZAFCOPZNZDPZADPURUSFELZVAVDQURVEUSCEFGHRSZAFBCVBDE
      GVBTZIJUAUBUTVCADUTVCAUCUDZFCUEPZNZCUFPZNZAFVKNZAURUSVEVCVLQVFAFVICVKVBEG
      VKTZVITZVGUGUBURVLVMQUSURVJFAVKURVHUHLVJFQUCUIUJVHVICFVOHUKULUMSACVKEFGVN
      HUNUOUPUQ $.
      $( [6-Jan-2008] $) $( [4-Dec-2006] $)
  $}

  ${
    $d x y z U $.  $d x y z X $.
    imsdfn.1 $e |- X = ( Base ` U ) $.
    imsdfn.8 $e |- D = ( IndMet ` U ) $.
    $( Mapping for the induced metric distance function of a normed complex
       vector space. $)
    imsdf $p |- ( U e. NrmCVec -> D : ( X X. X ) --> RR ) $=
      ( cnv wcel cxp cr wf cnm cfv cnsb ccom fco eqid nvf nvmf sylanc imsval
      feq1d mpbird ) BFGZCCHZIAJUDIBKLZBMLZNZJZCIUEJUDCUFJUHUCUDCIUEUFOBUECDUEP
      ZQBUFCDUFPZRSUCUDIAUGABUFUEUJUIETUAUB $.
      $( [30-Sep-2007] $) $( [29-Nov-2006] $)

    $( Base set of the induced metric space of a normed complex vector
      space. $)
    imsba $p |- ( U e. NrmCVec -> X = dom dom D ) $=
      ( cnv wcel cxp cr wf cdm wceq imsdf fdm dmeq dmxpid syl6req 3syl ) BFGCCH
      ZIAJAKZSLZCTKZLABCDEMSIANUAUBSKCTSOCPQR $.
      $( [3-Oct-2007] $) $( [4-Sep-2007] $)

    ${
      imsbai.u $e |- U e. NrmCVec $.
      $( Base set of the induced metric space of a normed complex vector
        space. $)
      imsbai $p |- X = dom dom D $=
        ( cnv wcel cdm wceq imsba ax-mp ) BGHCAIIJFABCDEKL $.
        $( [22-Dec-2007] $) $( [19-Dec-2007] $)
    $}
  $}

  ${
    $d x y z D $.  $d x y z X $.
    imsmetlem.1 $e |- X = ( Base ` U ) $.
    imsmetlem.2 $e |- G = ( +v ` U ) $.
    imsmetlem.7 $e |- M = ( inv ` G ) $.
    imsmetlem.4 $e |- S = ( .s ` U ) $.
    imsmetlem.5 $e |- Z = ( 0v ` U ) $.
    imsmetlem.6 $e |- N = ( norm ` U ) $.
    imsmetlem.8 $e |- D = ( IndMet ` U ) $.
    imsmetlem.9 $e |- U e. NrmCVec $.
    $( Lemma for ~ imsmet . $)
    imsmetlem $p |- D e. Met $=
      ( vx vy vz cba cfv cvv fvex eqeltr cnv wcel cxp cr wf imsdf ax-mp cv wa
      co cc0 wceq c1 cneg imsdval2 mp3an1 eqeq1d wb nvgcl cc ax1cn negcl nvscl
      mp3an12 sylan2 nvz mpan syl nvzcl w3a nvrcan mp3an2 pm3.27 sylanc nvass
      pm3.26 adantl syl3anc nvlinv opreq2d nv0rid adantr 3eqtrd nv0lid eqeq12d
      bitr3d 3bitrd caddc cle wbr nvtri 3adant3 3adant2 3adant1 3simp1
      3ad2ant3 opreq1d eqtr3d fveq2d eqtr4d nvdif eqtrd opreq12d 3brtr4d 3coml
      ismeti ) QRSAGGCTUAUBICTUCUDCUEUFZGGUGUHAUIPACGIOUJUKQULZGUFZRULZGUFZUMZX
      LXNAUNZUOUPXLUQURZXNBUNZDUNZFUAZUOUPZXTHUPZXLXNUPZXPXQYAUOXKXMXOXQYAUPZPX
      LXNABCDFGIJLNOUSUTZVAXPXTGUFZYBYCVBZXMXSGUFZYGXOXKXMYIYGPXLXSCDGIJVCUTXKX
      RVDUFZXOYIPUQVEVFZXRXNBCGILVGVHZVIZXKYGYHPXTCFGHIMNVJVKVLXPXTXNDUNZHXNDUN
      ZUPZYCYDYGXOYPYCVBZXPYGHGUFZXOYQXKYRPCGHIMVMUKXKYGYRXOVNYQPXTHXNCDGIJVOVK
      VPYMXMXOVQZVRXPYNXLYOXNXPYNXLXSXNDUNZDUNZXLHDUNZXLXMYIXOYNUUAUPZXPXKXMYIX
      OVNUUCPXLXSXNCDGIJVSVKXMXOVTXOYIXMYLWAYSWBXPYTHXLDXOYTHUPZXMXKXOUUDPXNBCD
      GHIJLMWCVKWAWDXMUUBXLUPZXOXKXMUUEPXLCDGHIJMWEVKZWFWGXOYOXNUPZXMXKXOUUGPXN
      CDGHIJMWHVKWAWIWJWKSULZGUFZXMXOXQUUHXLAUNZUUHXNAUNZWLUNZWMWNUUIXMXOVNZXLX
      RUUHBUNZDUNZUUHXSDUNZDUNZFUAZUUOFUAZUUPFUAZWLUNZXQUULWMUUOGUFZUUPGUFZUURU
      VAWMWNZUUMXKUVBUVCUVDPUUOUUPCDFGIJNWOUTUUIXMUVBXOXMUUNGUFZUVBUUIXMUMZXKXM
      UVEUVBPXLUUNCDGIJVCUTUUIXMVQZUUIUVEXMXKYJUUIUVEPYKXRUUHBCGILVGVHWFZVRWPZU
      UIXOUVCXMUUIYIUVCXOXKUUIYIUVCPUUHXSCDGIJVCUTYLVIWQVRUUMXQYAUURXMXOYEUUIYF
      WRUUMUUQXTFUUMUUOUUHDUNZXSDUNZUUQXTUVBUUIYIUVKUUQUPZUUMXKUVBUUIYIVNUVLPUU
      OUUHXSCDGIJVSVKUVIUUIXMXOWSXOUUIYIXMYLWTWBUUMUVJXLXSDUUIXMUVJXLUPXOUVFUVJ
      XLUUNUUHDUNZDUNZUUBXLXMUVEUUIUVJUVNUPZUVFXKXMUVEUUIVNUVOPXLUUNUUHCDGIJVSV
      KUVGUVHUUIXMVTWBUVFUVMHXLDUUIUVMHUPZXMXKUUIUVPPUUHBCDGHIJLMWCVKWFWDXMUUEU
      UIUUFWAWGWPXAXBXCXDUUMUUJUUSUUKUUTWLUUIXMUUJUUSUPXOUVFUUJUUHXRXLBUNDUNFUA
      ZUUSXKUUIXMUUJUVQUPPUUHXLABCDFGIJLNOUSUTXKUUIXMUVQUUSUPPUUHXLBCDFGIJLNXEU
      TXFWPUUIXOUUKUUTUPZXMXKUUIXOUVRPUUHXNABCDFGIJLNOUSUTWQXGXHXIXJ $.
      $( [2-Oct-2007] $) $( [29-Nov-2006] $)
  $}

  ${
    imsmet.8 $e |- D = ( IndMet ` U ) $.
    $( The induced metric of a normed complex vector space is a metric space.
       Part of Definition 2.2-1 of [Kreyszig] p. 58. $)
    imsmet $p |- ( U e. NrmCVec -> D e. Met ) $=
      ( cnv wcel cims cfv cme caddc cmul cop cabs cif wceq fveq2 eleq1d cns
      cpv cgn cnm cba cn0v eqid elimnvu imsmetlem dedth syl5eqel ) BDEZBFGZHAUH
      UIHEUHBIJKLKZMZFGZHEBUJBUKNUIULHBUKFOPULUKQGZUKUKRGZUNSGZUKTGZUKUAGZUKUBG
      ZUQUCUNUCUOUCUMUCURUCUPUCULUCBUDUEUFCUG $.
      $( [2-Oct-2007] $) $( [4-Dec-2006] $)
  $}

  ${
    nvelbl.1 $e |- X = ( Base ` U ) $.
    nvelbl.3 $e |- M = ( -v ` U ) $.
    nvelbl.6 $e |- N = ( norm ` U ) $.
    nvelbl.8 $e |- D = ( IndMet ` U ) $.
    $( Membership of a vector in a ball. $)
    nvelbl $p |- ( ( ( U e. NrmCVec /\ P e. X /\ A e. X ) /\ ( R e. RR /\
      0 < R ) ) -> ( A e. ( P ( ball ` D ) R ) <-> ( N ` ( A M P ) ) < R ) ) $=
      ( cnv wcel w3a cr cc0 clt wbr wa cbl cfv co cme cdm wb eqid elbl3 imsmet
      3ad2ant1 imsba eleq2d biimpa 3adant3 3adant2 3jca sylan wceq imsdval
      3com23 breq1d adantr bitrd ) EMNZCHNZAHNZOZDPNQDRSTZTACDBUAUBUCNZACBUCZDR
      SZACFUCGUBZDRSZBUDNZCBUEUEZNZAVONZOVHVIVKUFVGABCDVOVOUGUHVGVNVPVQVDVEVNVF
      BELUIUJVDVEVPVFVDVEVPVDHVOCBEHILUKZULUMUNVDVFVQVEVDVFVQVDHVOAVRULUMUOUPUQ
      VGVKVMUFVHVGVJVLDRVDVFVEVJVLURACBEFGHIJKLUSUTVAVBVC $.
      $( [27-Dec-2007] $) $( [27-Dec-2007] $)
  $}

  ${
    nvelbl2.1 $e |- X = ( Base ` U ) $.
    nvelbl2.2 $e |- G = ( +v ` U ) $.
    nvelbl2.6 $e |- N = ( norm ` U ) $.
    nvelbl2.8 $e |- D = ( IndMet ` U ) $.
    $( Membership of an off-center vector in a ball. $)
    nvelbl2 $p |- ( ( ( U e. NrmCVec /\ P e. X /\ A e. X ) /\ ( R e. RR /\
      0 < R ) ) -> ( ( P G A ) e. ( P ( ball ` D ) R ) <-> ( N ` A ) < R ) ) $=
      ( cnv wcel w3a cr cc0 clt wbr wa co cbl cfv cnsb wb eqid nvelbl id nvgcl
      syl3dan3 sylan nvpncan2 fveq2d breq1d adantr bitrd ) EMNZCHNZAHNZOZDPNQDR
      STZTCAFUAZCDBUBUCUANZVBCEUDUCZUAZGUCZDRSZAGUCZDRSZUQURVBHNZOZVAVCVGUEUTVB
      BCDEVDGHIVDUFZKLUGUQURVJVKUSVKUHCAEFHIJUIUJUKUTVGVIUEVAUTVFVHDRUTVEAGCAEF
      VDHIJVLULUMUNUOUP $.
      $( [9-Jan-2008] $) $( [27-Dec-2007] $)
  $}

  ${
    nvcnf.1 $e |- X = ( Base ` U ) $.
    nvcnf.2 $e |- Y = ( Base ` W ) $.
    nvcnf.8 $e |- C = ( IndMet ` U ) $.
    nvcnf.d $e |- D = ( IndMet ` W ) $.
    nvcnf.3 $e |- J = ( Open ` C ) $.
    nvcnf.4 $e |- K = ( Open ` D ) $.
    $( MAY BE REVISED - not used? $)
    $( A continuous function is an operation (normed complex vector space
       version of ~ cnf ). $)
    nvcnf $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\
                F e. ( J Cn K ) ) -> F : X --> Y ) $=
      ( cnv wcel ccn co w3a wf cdm cme eqid metcnf imsmet id syl3an wb wceq
      imsba feq2 syl feq3 sylan9bb 3adant3 mpbird ) CPQZGPQZDEFRSQZTHIDUAZAUBUB
      ZBUBUBZDUAZAUCQBUCQUTVDURUSUTABDEFVBVCVBUDVCUDNOUEACLUFBGMUFUTUGUHURUSVAV
      DUIUTURVAVBIDUAZUSVDURHVBUJVAVEUIACHJLUKHVBIDULUMUSIVCUJVEVDUIBGIKMUKIVCV
      BDUNUMUOUPUQ $.
      $( [30-Jan-2008] $) $( [13-Dec-2007] $)

    $( MAY BE REVISED - delete this if not used? $)
    $( A continuous function is an operation (normed complex vector space
       version of ~ cnpf ). $)
    nvcnpf $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ P e. X ) /\
                F e. ( ( J CnP K ) ` P ) ) -> F : X --> Y ) $=
      ( cnv wcel w3a ccnp co cfv wa wf cdm cme eqid metcnpf imsmet 3ad2ant1
      3ad2ant2 imsba eleq2d biimpa 3adant2 3jca sylan wb wceq feq2 syl feq3
      sylan9bb 3adant3 adantr mpbird ) DQRZHQRZCIRZSZECFGTUAUBRZUCIJEUDZAUEUEZB
      UEUEZEUDZAUFRZBUFRZCVMRZSVKVOVJABCEFGVMVNVMUGVNUGOPUHVJVPVQVRVGVHVPVIADMU
      IUJVHVGVQVIBHNUIUKVGVIVRVHVGVIVRVGIVMCADIKMULZUMUNUOUPUQVJVLVOURZVKVGVHVT
      VIVGVLVMJEUDZVHVOVGIVMUSVLWAURVSIVMJEUTVAVHJVNUSWAVOURBHJLNULJVNVMEVBVAVC
      VDVEVF $.
      $( [?] $) $( [16-Dec-2007] $)
  $}

  ${
    $d x y A $.  $d x y C $.  $d x y D $.  $d x y F $.  $d x y J $.
    $d x y K $.  $d x y P $.  $d x y U $.  $d x y W $.  $d x y X $.
    nvcni.1 $e |- X = ( Base ` U ) $.
    nvcni.m $e |- M = ( norm ` U ) $.
    nvcni.n $e |- N = ( norm ` W ) $.
    nvcni.r $e |- R = ( -v ` U ) $.
    nvcni.s $e |- S = ( -v ` W ) $.
    nvcni.8 $e |- C = ( IndMet ` U ) $.
    nvcni.d $e |- D = ( IndMet ` W ) $.
    nvcni.j $e |- J = ( Open ` C ) $.
    nvcni.k $e |- K = ( Open ` D ) $.
    $( MAY BE REVISED - delete this if not used $)
    $( Epsilon-delta property of a continuous operator.  (Normed complex vector
       space version of ~ metcni .) $)
    nvcni $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ F e. ( J Cn K ) ) /\
        ( P e. X /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\ A. y e. X
      ( ( M ` ( P R y ) ) < x -> ( N ` ( ( F ` P ) S ( F ` y ) ) ) < A ) ) ) $=
      ( cnv wcel ccn co w3a cr cc0 clt wbr wa cv cfv wi wral wrex cdm cme eqid
      metcni ex imsmet id syl3an wb imsba eleq2d 3anbi1d raleq1d anbi2d
      rexbidv imbi12d 3ad2ant1 mpbird imp cba wf wceq imsdval 3expb breq1d
      3ad2antl1 3simp2 adantr ffvelrn ad2ant2lr 3adantl1 ad2ant2l syl3anc
      anassrs ralbidva nvcnf syl3dan3 3ad2antr1 mpbid ) IUFUGZOUFUGZJKLUHUIUGZU
      JZFPUGZCUKUGZULCUMUNZUJZUOULAUPZUMUNZFBUPZDUIZXHUMUNZFJUQZXJJUQZEUIZCUMUN
      ZURZBPUSZUOZAUKUTZXIFXJGUIMUQZXHUMUNZXMXNHUINUQZCUMUNZURZBPUSZUOZAUKUTZXC
      XGXTXCXGXTURZFDVAVAZUGZXEXFUJZXIXQBYJUSZUOZAUKUTZURZDVBUGZEVBUGZXBYPWTXAX
      BYQYRXBUJYLYOABCDEFJKLYJEVAVAZYJVCUDYSVCUEVDVEDIUBVFEOUCVFXBVGVHWTXAYIYPV
      IXBWTXGYLXTYOWTXDYKXEXFWTPYJFDIPQUBVJZVKVLWTXSYNAUKWTXRYMXIWTXQBPYJYTVMVN
      VOVPVQVRVSXCXEXDXTYHVIZXFXCXDUUAWTXAPOVTUQZJWAZXDUUAURXBWTXAUUCUJZXDUUAUU
      DXDUOZXSYGAUKUUEXRYFXIUUEXQYEBPUUDXDXJPUGZXQYEVIUUDXDUUFUOZUOZXLYBXPYDWTX
      AUUGXLYBVIUUCWTUUGUOXKYAXHUMWTXDUUFXKYAWBFXJDIGMPQTRUBWCWDWEWFUUHXOYCCUMX
      AXMUUBUGZXNUUBUGZXOYCWBUUHXMXNEOHNUUBUUBVCZUASUCWCUUDXAUUGWTXAUUCWGWHXAUU
      CUUGUUIWTUUCXDUUIXAUUFPUUBFJWIWJWKXAUUCUUGUUJWTUUCUUFUUJXAXDPUUBXJJWIWLWK
      WMWEVPWNWOVNVOVEDEIJKLOPUUBQUUKUBUCUDUEWPWQVSWRWS $.
      $( [16-Feb-2008] $) $( [13-Dec-2007] $)

    $( MAY BE REVISED - delete this if not used $)
    $( Epsilon-delta property of a continuous operator.  (Normed complex vector
       space version of ~ metcni .) $)
    nvcni2 $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ F e. ( J Cn K ) ) /\
        ( P e. X /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\ A. y e. X
    ( ( M ` ( P R y ) ) <_ x -> ( N ` ( ( F ` P ) S ( F ` y ) ) ) <_ A ) ) ) $=
      ( cnv wcel ccn co w3a cr cc0 clt wbr wa cv cle cfv wi wral wrex cdm cme
      eqid metcni2 ex imsmet id syl3an wb imsba eleq2d 3anbi1d raleq1d anbi2d
      rexbidv imbi12d 3ad2ant1 mpbird imp cba wf wceq imsdval 3expb breq1d
      3ad2antl1 3simp2 adantr ffvelrn ad2ant2lr 3adantl1 ad2ant2l syl3anc
      anassrs ralbidva nvcnf syl3dan3 3ad2antr1 mpbid ) IUFUGZOUFUGZJKLUHUIUGZU
      JZFPUGZCUKUGZULCUMUNZUJZUOULAUPZUMUNZFBUPZDUIZXIUQUNZFJURZXKJURZEUIZCUQUN
      ZUSZBPUTZUOZAUKVAZXJFXKGUIMURZXIUQUNZXNXOHUINURZCUQUNZUSZBPUTZUOZAUKVAZXD
      XHYAXDXHYAUSZFDVBVBZUGZXFXGUJZXJXRBYKUTZUOZAUKVAZUSZDVCUGZEVCUGZXCYQXAXBX
      CYRYSXCUJYMYPABCDEFJKLYKEVBVBZYKVDUDYTVDUEVEVFDIUBVGEOUCVGXCVHVIXAXBYJYQV
      JXCXAXHYMYAYPXAXEYLXFXGXAPYKFDIPQUBVKZVLVMXAXTYOAUKXAXSYNXJXAXRBPYKUUAVNV
      OVPVQVRVSVTXDXFXEYAYIVJZXGXDXEUUBXAXBPOWAURZJWBZXEUUBUSXCXAXBUUDUJZXEUUBU
      UEXEUOZXTYHAUKUUFXSYGXJUUFXRYFBPUUEXEXKPUGZXRYFVJUUEXEUUGUOZUOZXMYCXQYEXA
      XBUUHXMYCVJUUDXAUUHUOXLYBXIUQXAXEUUGXLYBWCFXKDIGMPQTRUBWDWEWFWGUUIXPYDCUQ
      XBXNUUCUGZXOUUCUGZXPYDWCUUIXNXOEOHNUUCUUCVDZUASUCWDUUEXBUUHXAXBUUDWHWIXBU
      UDUUHUUJXAUUDXEUUJXBUUGPUUCFJWJWKWLXBUUDUUHUUKXAUUDUUGUUKXBXEPUUCXKJWJWMW
      LWNWFVQWOWPVOVPVFDEIJKLOPUUCQUULUBUCUDUEWQWRVTWSWT $.
      $( [?] $) $( [13-Dec-2007] $)

    $( MAY BE REVISED - create metcni3 w/ swapped distance args, and replace
       nvcni above w/ proof from that, and make nvcni3 obsolete. $)
    $( Epsilon-delta property of a continuous operator.  (Normed complex vector
       space version of ~ metcni .) $)
    nvcni3 $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ F e. ( J Cn K ) ) /\
        ( P e. X /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\ A. y e. X
      ( ( M ` ( y R P ) ) < x -> ( N ` ( ( F ` y ) S ( F ` P ) ) ) < A ) ) ) $=
      ( cnv wcel ccn co w3a cr cc0 clt wbr wa cv cfv wi wral wrex nvcni wb
      nvsub breq1d 3expb 3ad2antl1 wceq cba wf eqid ffvelrn anim12i anandis
      sylan2 anassrs ex 3adant1 nvcnf syl3dan3 imp imbi12d ralbidva anbi2d
      rexbidv 3ad2antr1 mpbid ) IUFUGZOUFUGZJKLUHUIUGZUJZFPUGZCUKUGZULCUMUNZUJU
      OULAUPZUMUNZFBUPZGUIMUQZWNUMUNZFJUQZWPJUQZHUINUQZCUMUNZURZBPUSZUOZAUKUTZW
      OWPFGUIMUQZWNUMUNZWTWSHUINUQZCUMUNZURZBPUSZUOZAUKUTZABCDEFGHIJKLMNOPQRSTU
      AUBUCUDUEVAWJWLWKXFXNVBWMWJWKUOZXEXMAUKXOXDXLWOXOXCXKBPWJWKWPPUGZXCXKVBWJ
      WKXPUOZUOZWRXHXBXJWGWHXQWRXHVBZWIWGWKXPXSWGWKXPUJWQXGWNUMFWPIGMPQTRVCVDVE
      VFXRXAXICUMWJXQXAXIVGZWGWHPOVHUQZJVIZXQXTURZWIWHYBYCWGWHYBUOXQXTWHYBXQXTW
      HWSYAUGZWTYAUGZUOZXTYBXQUOWHYDYEXTWSWTOHNYAYAVJZUASVCVEYBWKXPYFYBWKUOYDYB
      XPUOYEPYAFJVKPYAWPJVKVLVMVNVOVPVQDEIJKLOPYAQYGUBUCUDUEVRVSVTVDWAVOWBWCWDW
      EWF $.
      $( [?] $) $( [13-Dec-2007] $)
  $}

  ${
    nvlmcl.1 $e |- X = ( Base ` U ) $.
    nvlmcl.2 $e |- D = ( IndMet ` U ) $.
    nvlmcl.3 $e |- P e. V $.
    $( Closure of the limit of a converging vector sequence. $)
    nvlmcl $p |- ( ( U e. NrmCVec /\ F ( ~~>m ` D ) P ) -> P e. X ) $=
      ( cnv wcel clm cfv wbr wa cdm cme cvv eqid lmcl mp3an2 imsmet sylan wceq
      imsba adantr eleqtrrd ) CIJZDBAKLMZNBAOOZEAPJZUHBUIJZUGUJBQJUHUKHQABDUIUI
      RSTACGUAUBUGEUIUCUHACEFGUDUEUF $.
      $( [6-Jan-2008] $) $( [26-Dec-2007] $)
  $}

  ${
    $d k D $.  $d k F $.  $d k P $.  $d k R $.  $d k U $.  $d k X $.
    nvlmle.1 $e |- X = ( Base ` U ) $.
    nvlmle.6 $e |- N = ( norm ` U ) $.
    nvlmle.8 $e |- D = ( IndMet ` U ) $.
    nvlmle.p $e |- P e. V $.
    $( If the norm of each member of a converging sequence is less than or
       equal to a given amount, so is the norm of the convergence value. $)
    nvlmle $p |- ( ( ( U e. NrmCVec /\ F : NN --> X /\ F ( ~~>m ` D ) P ) /\
     ( R e. RR /\ A. k e. NN ( N ` ( F ` k ) ) <_ R ) ) -> ( N ` P ) <_ R ) $=
      ( cnv wcel cn wf clm cfv wbr w3a cr cv cle wral wa cn0v co wceq nvlmcl
      eqid nvnd syldan 3adant2 adantr cme cdm cvv c1 1z nnuz lmle mp3anl2
      imsmet anim1i nvzcl imsba eleqtrd 3ad2ant1 simprl ffvelrn sylan2 anassrs
      breq1d biimpd r19.20dva imp adantrl 3adantl3 3jca sylanc eqbrtrd ) DMNZOH
      FPZFBAQRSZTZCUANZEUBZFRZGRZCUCSZEOUDZUEZUEZBGRZBDUFRZAUGZCUCWEWNWPUHZWLWB
      WDWQWCWBWDBHNWQABDFHIKLUIBADGHWOIWOUJZJKUKULUMUNAUONZWDUEZWOAUPUPZNZWFWHW
      OAUGZCUCSZEOUDZTZWPCUCSZWMWSBUQNWDXFXGLUQABWOCEFURXAOXAUJUSUTVAVBWEWTWLWB
      WDWTWCWBWSWDADKVCVDUMUNWMXBWFXEWEXBWLWBWCXBWDWBWOHXADHWOIWRVEADHIKVFVGVHU
      NWEWFWKVIWBWCWLXEWDWBWCUEZWKXEWFXHWKXEXHWJXDEOXHWGONZUEZWJXDXJWIXCCUCWBWC
      XIWIXCUHZWBWHHNXKWCXIUEWHADGHWOIWRJKUKOHWGFVJVKVLVMVNVOVPVQVRVSVTWA $.
      $( [9-Jan-2008] $) $( [25-Dec-2007] $)
  $}

  ${
    $d t u v w x y z $.
    cnims.6 $e |- U = <. <. + , x. >. , abs >. $.
    cnims.7 $e |- D = ( abs o. - ) $.
    $( The metric induced on the complex numbers.  ~ cnmet proves that it is
       a metric.  (Contributed by Steve Rodriguez, 5-Dec-2006; revised by nm
       15-Jan-2008.) $)
    cnims $p |- D = ( IndMet ` U ) $=
      ( cabs cmin ccom cims cfv cnv wcel wceq cnnv cnnvm cnnvnm eqid imsval
      ax-mp eqtr4 ) AEFGZBHIZDBJKUATLBCMUABFEBCNBCOUAPQRS $.
      $( [15-Jan-2008] $) $( [28-Nov-2006] $)
  $}

  ${
    $d f u v w z F $.  $d f z D $.  $d z J $.  $d f u x y z $.
    sqcn.2 $e |- D = ( IndMet ` <. <. + , x. >. , abs >. ) $.
    sqcn.j $e |- J = ( Open ` D ) $.
    sqcn.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( x ^ 2 ) ) } $.
    $( MAY BE REVISED - make obsolete; use sqcn2. $)
    $( The square function on complex numbers is continuous. $)
    sqcn $p |- F e. ( J Cn J ) $=
      ( vf vz vw vv vu ccn co wcel cn cc cv wf clm cfv wbr wceq wa copab wi
      wral cme wal wb caddc cmul cop cabs cims cmin ccom eqid cnims eqtr4
      cnmet c2 cexp sqclt fopab cnmetba metcn4 mp3an cli c1 cz w3a cuz visset
      nnex opabex2 climmul id ancli ffvelrn fveq2 fveq2d fvex fvopab4 adantl
      opreq1 oprex sqvalt eqtrd syl 3jca elnnuz sylan2br r19.21aiva 1z jctil
      syl2an adantrl ad2antrl breqtrrd expcom lmclimnn ffvelrni fopab2 sylib
      cvv mpan 3imtr4d ancoms mpgbir ) DEENOPZQRISZTZXMJSZCUAUBZUCZKSZQPZLSXRXM
      UBZDUBZUDUEKLUFZXODUBZXPUCZUGZJRUHUGZICUIPZYGRRDTXLYFIUJUKCCULUMUNUOUNZUP
      UBUOUQURZFYIYHYHUSYIUSUTVAZVBZYKABRRASZVCVDOZDHYLVEVFZJLCCIKDYBEERRCYJVGZ
      YOGGYBUSZVHVIXNYEJRXORPZXNYEYQXNUEZXMXOVJUCZYBYCVJUCZXQYDYSYRYTYSYRUEYBXO
      XOUMOZYCVJYSXNYBUUAVJUCZYQYSYSUEVKVLPZMSZXMUBZRPZUUFUUDYBUBZUUEUUEUMOZUDZ
      VMZMVKVNUBZUHZUEUUBYSXNXOXOMXMXMYBVKIVOZUUMKLQYAVPVQJVOZUUNVRYSYSYSVSVTXN
      UULUUCXNUUJMUUKXNUUDQPZUUJUUDUUKPXNUUOUEZUUFUUFUUIQRUUDXMWAZUUQUUPUUGUUED
      UBZUUHUUOUUGUURUDXNKLUUDYAUURQYBXRUUDUDXTUUEDXRUUDXMWBWCYPUUEDWDWEWFUUPUU
      FUURUUHUDUUQUUFUURUUEVCVDOZUUHABUUEYMUUSRDYLUUEVCVDWGHUUEVCVDWHWEUUEWIWJW
      KWJWLUUDWMWNWOWPWQWRWSYQYCUUAUDYSXNYQYCXOVCVDOZUUAABXOYMUUTRDYLXOVCVDWGHX
      OVCVDWHWEXOWIWJWTXAXBRCXOXMYJXCXNYDYTUKZYQXNQRYBTZUVAXNYARPZKQUHUVBXNUVCK
      QXNXSUEXTRPUVCQRXRXMWARRXTDYNXDWKWOKLQRYAYBYPXEXFYCXGPUVBUVAXODWDXGCYCYBY
      JXCXHWKWFXIXJWOXK $.
      $( [30-Nov-2007] $) $( [13-Jun-2007] $)
  $}

  ${
    $d w v $.
    sqcn2.1 $e |- D = ( abs o. - ) $.
    sqcn2.j $e |- J = ( Open ` D ) $.
    sqcn2.2 $e |- F = { <. w , v >. | ( w e. CC /\ v = ( w ^ 2 ) ) } $.
    $( The square function on complex numbers is continuous. $)
    sqcn2 $p |- F e. ( J Cn J ) $=
      ( caddc cmul cop cabs eqid cnims sqcn ) ABCDECIJKLKZPMFNGHO $.
      $( [1-Dec-2007] $) $( [18-Aug-2007] $)
  $}

  ${
    $d t u v w C $.  $d t u v w D $.  $d t u v w J $.  $d v w K $.
    $d t u v w N $.  $d t u v w X $.
    nmcnilem.1 $e |- X = ( Base ` U ) $.
    nmcnilem.2 $e |- G = ( +v ` U ) $.
    nmcnilem.4 $e |- S = ( .s ` U ) $.
    nmcnilem.6 $e |- N = ( norm ` U ) $.
    nmcnilem.8 $e |- C = ( IndMet ` U ) $.
    nmcnilem.7 $e |- D = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    nmcnilem.j $e |- J = ( Open ` C ) $.
    nmcnilem.k $e |- K = ( Open ` D ) $.
    nmcnilem.9 $e |- U e. NrmCVec $.
    $( Lemma for ~ nmcni . $)
    nmcnilem $p |- N e. ( J Cn K ) $=
      ( vv vu vw vt ccn co wcel cr wf cc0 cv clt wbr cfv wi wral wa wrex cme
      wb cnv imsmet ax-mp remet imsbai remetba metcn mp2an nvf wceq breq2
      imbi1d ralbidv anbi12d rcla4ev simplr pm3.27 c1 cneg cmin cabs cle nvabs
      mp3an1 adantlr lelttrt cc resubclt nvcl mpan syl2an recnd absclt syl
      nvgcl ax1cn negcl nvscl mp3an12 sylan2 syl3anc mpand w3a imsdval2 breq1d
      remetdval 3imtr4d r19.21aiva jca sylanc ex rgen2 mpbir2an ) HFGUCUDUEZIUF
      HUGZUHSUIZUJUKZUHTUIZUJUKZUAUIZUBUIZAUDZXPUJUKZXRHULZXSHULZBUDZXNUJUKZUMZ
      UBIUNZUOZTUFUPZUMZSUFUNUAIUNZAUQUEZBUQUEXLXMYKUOURDUSUEZYLRADNUTVABOVBUAS
      TUBABHFGIUFADIJNRVCPBOVDQVEVFYMXMRDHIJMVGVAYJUASIUFXRIUEZXNUFUEZUOZXOYIYO
      XOXTXNUJUKZYEUMZUBIUNZUOZYIYPXOUOZYHYTTXNUFXPXNVHZXQXOYGYSXPXNUHUJVIUUBYF
      YRUBIUUBYAYQYEXPXNXTUJVIVJVKVLVMYNYOXOVNUUAXOYSYPXOVOUUAYRUBIUUAXSIUEZUOZ
      XRVPVQZXSCUDZEUDZHULZXNUJUKZYBYCVRUDZVSULZXNUJUKZYQYEUUDUUKUUHVTUKZUUIUUL
      YPUUCUUMXOYNUUCUUMYOYMYNUUCUUMRXRXSCDEHIJKLMWAWBWCWCYPUUCUUMUUIUOUULUMZXO
      UUKUFUEZUUHUFUEZYOUUNYPUUCUOUUKUUHXNWDYNUUCUUOYOYNUUCUOZUUJWEUEUUOUUQUUJY
      BUFUEZYCUFUEZUUJUFUEYNUUCYBYCWFYMYNUURRXRDHIJMWGWHZYMUUCUUSRXSDHIJMWGWHZW
      IWJUUJWKWLWCYNUUCUUPYOUUQUUGIUEZUUPYNUUFIUEZUVBUUCYMYNUVCUVBRXRUUFDEIJKWM
      WBYMUUEWEUEUUCUVCRVPWNWOUUEXSCDIJLWPWQWRYMUVBUUPRUUGDHIJMWGWHWLWCYNYOUUCV
      NWSWCWTYPUUCYQUUIURZXOYNUUCUVDYOYMYNUUCUVDRYMYNUUCXAXTUUHXNUJXRXSACDEHIJK
      LMNXBXCWBWCWCYPUUCYEUULURZXOYNUUCUVEYOUUQYDUUKXNUJUURUUSYDUUKVHYNUUCYBYCB
      OXDUUTUVAWIXCWCWCXEXFXGXHXIXJXK $.
      $( [17-Nov-2007] $) $( [16-May-2007] $)
  $}

  ${
    nmcn.6 $e |- N = ( norm ` U ) $.
    nmcn.8 $e |- C = ( IndMet ` U ) $.
    nmcn.7 $e |- D = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    nmcn.j $e |- J = ( Open ` C ) $.
    nmcn.k $e |- K = ( Open ` D ) $.
    ${
      nmcni.9 $e |- U e. NrmCVec $.
      $( The norm of a normed complex vector space is a continuous function. $)
      nmcni $p |- N e. ( J Cn K ) $=
        ( cns cfv cpv cba eqid nmcnilem ) ABCMNZCCONZDEFCPNZUAQTQSQGHIJKLR $.
        $( [17-Nov-2007] $) $( [16-May-2007] $)
    $}

    $( The norm of a normed complex vector space is a continuous function. $)
    nmcn $p |- ( U e. NrmCVec -> N e. ( J Cn K ) ) $=
      ( cnv wcel ccn co caddc cmul cop cabs cif cnm cfv cims copn wceq fveq2
      syl5eq fveq2d opreq1d eleq12d eqid elimnvu nmcni dedth ) CLMZFDENOZMUOCPQ
      RSRZTZUAUBZURUCUBZUDUBZENOZMCUQCURUEZFUSUPVBVCCUAUBUSFCURUAUFGUGVCDVAENVC
      AUDUBVADVCAUTUDVCCUCUBUTACURUCUFHUGUHJUGUIUJUTBURVAEUSUSUKUTUKIVAUKKCULUM
      UN $.
      $( [21-Nov-2007] $) $( [16-May-2007] $)
  $}

  ${
    nmcn2.6 $e |- N = ( norm ` U ) $.
    nmcn2.8 $e |- D = ( IndMet ` U ) $.
    nmcn2.j $e |- J = ( Open ` D ) $.
    $( MAY BE REVISED - delete if not used? $)
    $( The norm of a normed complex vector space is a continuous function to
       ` RR ` . $)
    nmcn2 $p |- ( U e. NrmCVec -> N e. ( J Cn ( topGen ` ran (,) ) ) ) $=
      ( cnv wcel cabs cmin ccom cr cxp cres copn cfv ccn co cioo crn ctg eqid
      nmcn tgioo opreq2i syl6eleqr ) BHIDCJKLMMNOZPQZRSCTUAUBQZRSAUHBCUIDEFUHUC
      ZGUIUCZUDUJUICRUHUIUKULUEUFUG $.
      $( [23-May-2008] $) $( [27-May-2007] $)

    ${
      nmcn3.k $e |- K = ( Open ` ( IndMet ` <. <. + , x. >. , abs >. ) ) $.
      $( MAY BE REVISED - delete if not used? $)
      $( The norm of a normed complex vector space is a continuous function to
         ` CC ` . $)
      nmcn3 $p |- ( U e. NrmCVec -> N e. ( J Cn K ) ) $=
        ( cme wcel cabs cmin ccom cr cxp cres caddc cmul cop cims cfv w3a wss
        wa copn ccn co cnv eqid metcnss sseld imsmet remet a1i cnims eqcomi
        cnmet 3jca resss sseqtr jctir nmcn sylc ) AJKZLMNZOOPZQZJKZRSTLTZUAUBZJ
        KZUCZVHVKUDZUEZECVHUFUBZUGUHZKECDUGUHZKBUIKZVOVQVREAVHVKCVPDHVPUJZIUKUL
        VSVMVNVSVEVIVLABGUMVIVSVHVHUJZUNUOVLVSVKVFVKVFVJVJUJVFUJUPZUQURUOUSVHVF
        VKVFVGUTWBVAVBAVHBCVPEFGWAHVTVCVD $.
        $( [?] $) $( [30-May-2007] $)
    $}
  $}

  ${
    nmcnc.6 $e |- N = ( norm ` U ) $.
    nmcnc.8 $e |- C = ( IndMet ` U ) $.
    nmcnc.7 $e |- D = ( abs o. - ) $.
    nmcnc.j $e |- J = ( Open ` C ) $.
    nmcnc.k $e |- K = ( Open ` D ) $.
    $( The norm of a normed complex vector space is a continuous function to
       ` CC ` .  (For ` RR ` , see ~ nmcn .) $)
    nmcnc $p |- ( U e. NrmCVec -> N e. ( J Cn K ) ) $=
      ( cme wcel cabs cmin ccom cr cxp cres w3a wss wa copn cfv ccn co cnv
      eqid metcnss sseld imsmet remet a1i cnmet 3jca resss sseqtr4 jctir nmcn
      sylc ) ALMZNOPZQQRZSZLMZBLMZTZVDBUAZUBZFDVDUCUDZUEUFZMFDEUEUFZMCUGMZVIVKV
      LFAVDBDVJEJVJUHZKUIUJVMVGVHVMVAVEVFACHUKVEVMVDVDUHZULUMVFVMBIUNUMUOVDVBBV
      BVCUPIUQURAVDCDVJFGHVOJVNUSUT $.
      $( [24-Nov-2007] $) $( [12-Aug-2007] $)
  $}

  ${
    abscn.1 $e |- C = ( abs o. - ) $.
    abscn.2 $e |- R = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    abscn.3 $e |- J = ( Open ` C ) $.
    abscn.4 $e |- K = ( Open ` R ) $.
    $( The absolute value function on complex numbers is continuous. $)
    abscn $p |- abs e. ( J Cn K ) $=
      ( caddc cmul cop cabs cnm cfv c2nd eqid nmfval opex cc cr wf cvv wcel
      absf axcnex fex mp2an op2nd eqtr2 cmin ccom cims cnims eqtr cnnv nmcni )
      ABIJKZLKZCDLURMNZURONLURUSUSPQUQLIJRSTLUASUBUCLUBUCUDUESTUBLUFUGUHUIALUJU
      KZURULNEUTURURPZUTPUMUNFGHURVAUOUP $.
      $( [?] $) $( [22-Aug-2007] $)
  $}

  ${
    $d r s t u v w A $.  $d r s t u D $.  $d r s t u F $.  $d v w G $.
    $d r s t u J $.  $d r s t u v w X $.
    va1cn.1 $e |- X = ( Base ` U ) $.
    va1cn.2 $e |- G = ( +v ` U ) $.
    va1cn.8 $e |- D = ( IndMet ` U ) $.
    va1cn.j $e |- J = ( Open ` D ) $.
    va1cn.f $e |- F = { <. w , v >. | ( w e. X /\ v = ( w G A ) ) } $.
    va1cn.9 $e |- U e. NrmCVec $.
    ${
      va1cnlem.6 $e |- N = ( norm ` U ) $.
      $( Lemma for ~ va1cn . $)
      va1cnlem $p |- ( A e. X -> F e. ( J Cn J ) ) $=
        ( vs vt vr vu wcel wf cc0 cv clt wbr co cfv wi wral wa cr wrex ccn cnv
        nvgcl mp3an1 ancoms r19.21aiva fopab2 sylib idd rgen wceq breq2 imbi1d
        ralbidv anbi12d rcla4ev mpanr2 adantll adantl wb opreq1 oprex fvopab4
        opreqan12d w3a cnsb cgr nvgrp ax-mp bafval eqid vsfval grppnpcan2 mpan
        fveq2d imsdval 3adant2 3adant1 sylanc 3adant3 3eqtr4d 3comr 3expa
        eqtrd breq1d imbi2d ralbidva anbi2d rexbidv adantrr mpbird exp32
        r19.21aivv jca cme imsmet imsbai metcn mp2an sylibr ) CJUBZJJFUCZUDRUEZ
        UFUGZUDSUEZUFUGZTUEZUAUEZDUHZXSUFUGZYAFUIZYBFUIZDUHZXQUFUGZUJZUAJUKZULZ
        SUMUNZUJZRUMUKTJUKZULZFHHUOUHUBZXOXPYNXOAUEZCGUHZJUBZAJUKXPXOYSAJYQJUBZ
        XOYSEUPUBZYTXOYSPYQCEGJKLUQURUSUTABJJYRFOVAVBXOYMTRJUMXOYAJUBZXQUMUBZUL
        ZXRYLXOUUDXRULZULYLXTYDYCXQUFUGZUJZUAJUKZULZSUMUNZUUEUUJXOUUCXRUUJUUBUU
        CXRUUFUUFUJZUAJUKZUUJUUKUAJYBJUBZUUFVCVDUUIXRUULULSXQUMXSXQVEZXTXRUUHUU
        LXSXQUDUFVFUUNUUGUUKUAJUUNYDUUFUUFXSXQYCUFVFVGVHVIVJVKVLVMXOUUDYLUUJVNZ
        XRXOUUBUUOUUCXOUUBULZYKUUISUMUUPYJUUHXTUUPYIUUGUAJUUPUUMULZYHUUFYDUUQYG
        YCXQUFUUQYGYACGUHZYBCGUHZDUHZYCUUBUUMYGUUTVEXOUUBUUMYEUURYFUUSDABYAYRUU
        RJFYQYACGVOOYACGVPVQABYBYRUUSJFYQYBCGVOOYBCGVPVQVRVLXOUUBUUMUUTYCVEZUUB
        UUMXOUVAUUBUUMXOVSZUURUUSEVTUIZUHZIUIZYAYBUVCUHZIUIZUUTYCUVBUVDUVFIGWAU
        BZUVBUVDUVFVEUUAUVHPEGLWBWCYAYBCUVCGJEGJKLWDEGUVCLUVCWEZWFWGWHWIUURJUBZ
        UUSJUBZUUTUVEVEZUVBUUAUVJUVKUVLPUURUUSDEUVCIJKUVIQMWJURUUBXOUVJUUMUUAUU
        BXOUVJPYACEGJKLUQURWKUUMXOUVKUUBUUAUUMXOUVKPYBCEGJKLUQURWLWMUUBUUMYCUVG
        VEZXOUUAUUBUUMUVMPYAYBDEUVCIJKUVIQMWJURWNWOWPWQWRWSWTXAXBXCXDXDXEXFXGXH
        DXIUBZUVNYPYOVNUUAUVNPDEMXJWCZUVOTRSUADDFHHJJDEJKMPXKZNUVPNXLXMXN $.
        $( [23-Nov-2007] $) $( [12-Aug-2007] $)
    $}

    $( Vector addition is continuous in its first operand. $)
      va1cn $p |- ( A e. X -> F e. ( J Cn J ) ) $=
        ( cnm cfv eqid va1cnlem ) ABCDEFGHEPQZIJKLMNOTRS $.
        $( [23-Nov-2007] $) $( [12-Aug-2007] $)
  $}

  ${
    $d t u v w A $.  $d r s t u C $.  $d r s t u D $.  $d r s t u F $.
    $d r s t u J $.  $d r s K $.  $d t u N $.  $d v w S $.  $d r s t u v w X $.
    sm1cni.1 $e |- X = ( Base ` U ) $.
    sm1cni.4 $e |- S = ( .s ` U ) $.
    sm1cni.7 $e |- C = ( abs o. - ) $.
    sm1cni.8 $e |- D = ( IndMet ` U ) $.
    sm1cni.j $e |- J = ( Open ` C ) $.
    sm1cni.k $e |- K = ( Open ` D ) $.
    sm1cni.f $e |- F = { <. w , v >. | ( w e. CC /\ v = ( w S A ) ) } $.
    sm1cni.9 $e |- U e. NrmCVec $.
    sm1cni.a $e |- A e. X $.
    ${
      sm1cni.2 $e |- G = ( +v ` U ) $.
      sm1cnilem.6 $e |- N = ( norm ` U ) $.
      $( Lemma for ~ sm1cni . $)
      sm1cnilem $p |- F e. ( J Cn K ) $=
        ( vs vt vr vu ccn co wcel cc wf cc0 cv clt wbr cfv wi wral wa cr wrex
        cme wb cnmet cnv imsmet ax-mp cnmetba imsbai metcn mp2an nvscl mp3an13
        fopab cmin cabs cmul wceq opreq2 breq1d imbi2d ralbidv anbi2d rexbidv
        cdiv wne breq2 imbi1d anbi12d rcla4ev nvcli redivclt mp3an2 adantll
        adantlr divgt0t mpanr1 cle nvge0 0re ltlen biimpr mpan sylan2 adantlll
        w3a ltmuldivt subclt absclt syl a1i simplr ad2antrr 3jca ad2antlr
        sylanc biimprd r19.21aiva jca c1 recnd mul01t eqbrtrd a1d lt01 1re
        pm2.61ne cnmetdval opreq1 oprex fvopab4 opreqan12d cneg imsdval2
        mp3an1 syl2an c1st cvc eqid nvvc vafval smfval bafval vcsubdir mp3an3
        fveq2d nvs 3eqtr2d eqtrd imbi12d ralbidva mpbird ex rgen2 mpbir2an ) HJ
        KUIUJUKZULMHUMZUNUEUOZUPUQZUNUFUOZUPUQZUGUOZUHUOZDUJZUVBUPUQZUVDHURZUVE
        HURZEUJZUUTUPUQZUSZUHULUTZVAZUFVBVCZUSZUEVBUTUGULUTZDVDUKEVDUKZUURUUSUV
        QVAVEDPVFGVGUKZUVRUAEGQVHVIUGUEUFUHDEHJKULMDPVJREGMNQUAVKSVLVMABULMAUOZ
        CFUJZHTUVSUVTULUKCMUKZUWAMUKUAUBUVTCFGMNOVNVOVPUVPUGUEULVBUVDULUKZUUTVB
        UKZVAZUVAUVOUWEUVAVAZUVOUVCUVDUVEVQUJZVRURZUVBUPUQZUWHCLURZVSUJZUUTUPUQ
        ZUSZUHULUTZVAZUFVBVCZUWFUWPUVCUWIUWHUNVSUJZUUTUPUQZUSZUHULUTZVAZUFVBVCZ
        UWJUNUWJUNVTZUWOUXAUFVBUXCUWNUWTUVCUXCUWMUWSUHULUXCUWLUWRUWIUXCUWKUWQUU
        TUPUWJUNUWHVSWAWBWCWDWEWFUUTUWJWGUJZVBUKZUNUXDUPUQZUWHUXDUPUQZUWLUSZUHU
        LUTZVAZUWPUWFUWJUNWHZVAZUWOUXJUFUXDVBUVBUXDVTZUVCUXFUWNUXIUVBUXDUNUPWIU
        XMUWMUXHUHULUXMUWIUXGUWLUVBUXDUWHUPWIWJWDWKWLUWEUXKUXEUVAUWDUXKUXEUWCUW
        DUWJVBUKZUXKUXECGLMNUDUAUBWMZUUTUWJWNWOWPWQUXLUXFUXIUWDUVAUXKUXFUWCUWDU
        VAVAZUNUWJUPUQZUXFUXKUXPUXNUXQUXFUXOUUTUWJWRWSUNUWJWTUQZUXKUXQUVSUWBUXR
        UAUBCGLMNUDXAVMUXQUXRUXKVAUNUWJXBUXOXCXDXEZXFXGUXLUXHUHULUXLUVEULUKZVAZ
        UWLUXGUWHVBUKZUXNUWDXHUXQUWLUXGVEUYAUWHUWJUUTXIUYAUYBUXNUWDUWFUXTUYBUXK
        UWEUXTUYBUVAUWCUXTUYBUWDUWCUXTVAZUWGULUKZUYBUVDUVEXJZUWGXKXLZWQWQWQUXNU
        YAUXOXMUWFUWDUXKUXTUWCUWDUVAXNXOXPUXKUXQUWFUXTUXSXQXRXSXTYAXRUWFUWHYBUP
        UQZUWRUSZUHULUTZUXBUWFUYHUHULUWFUXTVAZUWRUYGUYJUWQUNUUTUPUWEUXTUWQUNVTZ
        UVAUWCUXTUYKUWDUYCUWHULUKUYKUYCUWHUYFYCUWHYDXLWQWQUWEUVAUXTXNYEYFXTUNYB
        UPUQZUYIUXBYGYBVBUKUYLUYIVAZUXBYHUXAUYMUFYBVBUVBYBVTZUVCUYLUWTUYIUVBYBU
        NUPWIUYNUWSUYHUHULUYNUWIUYGUWRUVBYBUWHUPWIWJWDWKWLXEXEXLYIUWCUVOUWPVEUW
        DUVAUWCUVNUWOUFVBUWCUVMUWNUVCUWCUVLUWMUHULUYCUVGUWIUVKUWLUYCUVFUWHUVBUP
        UVDUVEDPYJWBUYCUVJUWKUUTUPUYCUVJUVDCFUJZUVECFUJZEUJZUWKUWCUXTUVHUYOUVIU
        YPEABUVDUWAUYOULHUVTUVDCFYKTUVDCFYLYMABUVEUWAUYPULHUVTUVECFYKTUVECFYLYM
        YNUYCUYQUYOYBYOUYPFUJIUJZLURZUWGCFUJZLURZUWKUYOMUKZUYPMUKZUYQUYSVTZUWCU
        XTUVSVUBVUCVUDUAUYOUYPEFGILMNUCOUDQYPYQUVSUWCUWBVUBUAUBUVDCFGMNOVNVOUVS
        UXTUWBVUCUAUBUVECFGMNOVNVOYRUYCUYTUYRLUWCUXTUWBUYTUYRVTZUBGYSURZYTUKZUW
        CUXTUWBXHVUEUVSVUGUAGVUFVUFUUAUUBVIUVDUVECFIVUFMGIUCUUCFGOUUDGIMNUCUUEU
        UFXEUUGUUHUYCUYDVUAUWKVTZUYEUVSUYDUWBVUHUAUBUWGCFGLMNOUDUUIVOXLUUJUUKWB
        UULUUMWEWFXOUUNUUOUUPUUQ $.
        $( [4-Dec-2007] $) $( [23-Aug-2007] $)
    $}

    $( Scalar multiplication is continuous in its first operand. $)
    sm1cni $p |- F e. ( J Cn K ) $=
      ( cpv cfv cnm eqid sm1cnilem ) ABCDEFGHGUAUBZIJGUCUBZKLMNOPQRSTUFUDUGUDUE
      $.
      $( [4-Dec-2007] $) $( [1-Aug-2007] $)
  $}


