$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Induced metric of a normed complex vector space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d u U $.
    imsval.3 $e |- M = ( -v ` U ) $.
    imsval.6 $e |- N = ( norm ` U ) $.
    imsval.8 $e |- D = ( IndMet ` U ) $.
    $( Value of the induced metric of a normed complex vector space. $)
    imsval $p |- ( U e. NrmCVec -> D = ( N o. M ) ) $=
      ( vu cnv wcel cims cfv cnm cnsb ccom cv wceq fveq2 coeq12d fvex coeq12i
      df-ims coex fvmpt 3eqtr4g ) BIJBKLBMLZBNLZOZADCOHBHPZMLZUINLZOUHIKUIBQUJU
      FUKUGUIBMRUIBNRSHUBUFUGBMTBNTUCUDGDUFCUGFEUAUE $.
      $( [16-Nov-2013] $) $( [11-Sep-2007] $)
  $}

  ${
    imsdval.1 $e |- X = ( BaseSet ` U ) $.
    imsdval.3 $e |- M = ( -v ` U ) $.
    imsdval.6 $e |- N = ( norm ` U ) $.
    imsdval.8 $e |- D = ( IndMet ` U ) $.
    $( Value of the induced metric (distance function) of a normed complex
       vector space.  Equation 1 of [Kreyszig] p. 59. $)
    imsdval $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                  ( A D B ) = ( N ` ( A M B ) ) ) $=
      ( wcel cfv co wceq 3ad2ant1 wfun cr wf syl cnv w3a cop ccom imsval fveq1d
      cdm nvf ffun cxp nvmf opelxpi 3adant1 eleqtrrd fvco syl3anc df-opr fveq2i
      fdm eqtrd 3eqtr4g ) DUALZAGLZBGLZUBZABUCZCMZVFEMZFMZABCNABENZFMVEVGVFFEUD
      ZMZVIVEVFCVKVBVCCVKOVDCDEFIJKUEPUFVEFQZEQZVFEUGZLVLVIOVBVCVMVDVBGRFSVMDFG
      HJUHGRFUITPVBVCVNVDVBGGUJZGESZVNDEGHIUKZVPGEUITPVEVFVPVOVCVDVFVPLVBABGGUL
      UMVBVCVOVPOZVDVBVQVSVRVPGEUSTPUNVFFEUOUPUTABCUQVJVHFABEUQURVA $.
      $( [11-Sep-2007] $)
  $}

  ${
    imsdval2.1 $e |- X = ( BaseSet ` U ) $.
    imsdval2.2 $e |- G = ( +v ` U ) $.
    imsdval2.4 $e |- S = ( .s ` U ) $.
    imsdval2.6 $e |- N = ( norm ` U ) $.
    imsdval2.8 $e |- D = ( IndMet ` U ) $.
    $( MAY BE REVISED $)
    $( Value of the distance function of the induced metric of a normed complex
       vector space.  Equation 1 of [Kreyszig] p. 59. $)
    imsdval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                  ( A D B ) = ( N ` ( A G ( -u 1 S B ) ) ) ) $=
      ( cnv wcel w3a co cnsb cfv c1 cneg eqid imsdval nvmval fveq2d eqtrd ) ENO
      AHOBHOPZABCQABERSZQZGSATUABDQFQZGSABCEUHGHIUHUBZLMUCUGUIUJGABDEFUHHIJKUKU
      DUEUF $.
      $( [11-Sep-2007] $) $( [28-Nov-2006] $)
  $}

  ${
    nvnd.1 $e |- X = ( BaseSet ` U ) $.
    nvnd.5 $e |- Z = ( 0vec ` U ) $.
    nvnd.6 $e |- N = ( norm ` U ) $.
    nvnd.8 $e |- D = ( IndMet ` U ) $.
    $( The norm of a normed complex vector space expressed in terms of the
       distance function of its induced metric.  Problem 1 of [Kreyszig]
       p. 63. $)
    nvnd $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( N ` A ) = ( A D Z ) ) $=
      ( cnv wcel wa co cfv wceq adantr eqid mpd3an3 c1 cnsb imsdval cneg nvmval
      nvzcl cns cc ax-1cn negcli nvsz mpan2 opreq2d nv0rid 3eqtrd fveq2d eqtr2d
      cpv ) CKLZAELZMZAFBNZAFCUAOZNZDOZADOURUSFELZVAVDPURVEUSCEFGHUEQZAFBCVBDEG
      VBRZIJUBSUTVCADUTVCATUCZFCUFOZNZCUQOZNZAFVKNZAURUSVEVCVLPVFAFVICVKVBEGVKR
      ZVIRZVGUDSURVLVMPUSURVJFAVKURVHUGLVJFPTUHUIVHVICFVOHUJUKULQACVKEFGVNHUMUN
      UOUP $.
      $( [6-Jan-2008] $) $( [4-Dec-2006] $)
  $}

  ${
    $d x y z U $.  $d x y z X $.
    imsdfn.1 $e |- X = ( BaseSet ` U ) $.
    imsdfn.8 $e |- D = ( IndMet ` U ) $.
    $( Mapping for the induced metric distance function of a normed complex
       vector space. $)
    imsdf $p |- ( U e. NrmCVec -> D : ( X X. X ) --> RR ) $=
      ( cnv wcel cxp cr wf cnm cfv cnsb ccom eqid nvf nvmf fco syl2anc imsval
      feq1d mpbird ) BFGZCCHZIAJUDIBKLZBMLZNZJZUCCIUEJUDCUFJUHBUECDUEOZPBUFCDUF
      OZQUDCIUEUFRSUCUDIAUGABUFUEUJUIETUAUB $.
      $( [30-Sep-2007] $) $( [29-Nov-2006] $)

    $( Base set of the induced metric space of a normed complex vector
       space. $)
    imsba $p |- ( U e. NrmCVec -> X = dom dom D ) $=
      ( cnv wcel cxp cr wf cdm wceq imsdf fdm dmeq dmxpid syl6req 3syl ) BFGCCH
      ZIAJAKZSLZCTKZLABCDEMSIANUAUBSKCTSOCPQR $.
      $( [4-Sep-2007] $)

    ${
      imsbai.u $e |- U e. NrmCVec $.
      $( Base set of the induced metric space of a normed complex vector
         space. $)
      imsbai $p |- X = dom dom D $=
        ( cnv wcel cdm wceq imsba ax-mp ) BGHCAIIJFABCDEKL $.
        $( [19-Dec-2007] $)
    $}
  $}

  ${
    $d x y z D $.  $d x y z X $.
    imsmetlem.1 $e |- X = ( BaseSet ` U ) $.
    imsmetlem.2 $e |- G = ( +v ` U ) $.
    imsmetlem.7 $e |- M = ( inv ` G ) $.
    imsmetlem.4 $e |- S = ( .s ` U ) $.
    imsmetlem.5 $e |- Z = ( 0vec ` U ) $.
    imsmetlem.6 $e |- N = ( norm ` U ) $.
    imsmetlem.8 $e |- D = ( IndMet ` U ) $.
    imsmetlem.9 $e |- U e. NrmCVec $.
    $( Lemma for ~ imsmet . $)
    imsmetlem $p |- D e. Met $=
      ( wcel co wceq mp3an1 vx vy vz cba cfv cvv eqeltri cnv cxp cr imsdf ax-mp
      fvex wf cv wa cc0 c1 cneg imsdval2 eqeq1d wb cc ax-1cn negcli nvscl nvgcl
      mp3an12 sylan2 nvz sylancr nvzcl nvrcan mpan mp3an2 sylancom simpl adantl
      w3a simpr nvass syl3anc nvlinv opreq2d nv0rid adantr 3eqtrd nv0lid bitr3d
      eqeq12d 3bitrd caddc cle wbr syl2anc 3adant3 3adant2 nvtri simp1 3ad2ant3
      3adant1 opreq1d eqtr3d fveq2d eqtr4d nvdif eqtrd opreq12d 3brtr4d ismeti
      3coml ) UAUBUCAGGCUDUEUFICUDUMUGCUHQZGGUIUJAUNPACGIOUKULUAUOZGQZUBUOZGQZU
      PZXMXOARZUQSXMURUSZXOBRZDRZFUEZUQSZYAHSZXMXOSZXQXRYBUQXLXNXPXRYBSZPXMXOAB
      CDFGIJLNOUTTZVAXQXLYAGQZYCYDVBPXPXNXTGQZYHXLXSVCQZXPYIPURVDVEZXSXOBCGILVF
      VHZXLXNYIYHPXMXTCDGIJVGTVIZYACFGHIMNVJVKXQYAXODRZHXODRZSZYDYEXNXPYHYPYDVB
      ZYMYHHGQZXPYQXLYRPCGHIMVLULXLYHYRXPVSYQPYAHXOCDGIJVMVNVOVPXQYNXMYOXOXQYNX
      MXTXODRZDRZXMHDRZXMXQXNYIXPYNYTSZXNXPVQXPYIXNYLVRXNXPVTXLXNYIXPVSUUBPXMXT
      XOCDGIJWAVNWBXQYSHXMDXPYSHSZXNXLXPUUCPXOBCDGHIJLMWCVNVRWDXNUUAXMSZXPXLXNU
      UDPXMCDGHIJMWEVNZWFWGXPYOXOSZXNXLXPUUFPXOCDGHIJMWHVNVRWJWIWKUCUOZGQZXNXPX
      RUUGXMARZUUGXOARZWLRZWMWNUUHXNXPVSZXMXSUUGBRZDRZUUGXTDRZDRZFUEZUUNFUEZUUO
      FUEZWLRZXRUUKWMUULUUNGQZUUOGQZUUQUUTWMWNZUUHXNUVAXPUUHXNUPZXNUUMGQZUVAUUH
      XNVTZUUHUVEXNXLYJUUHUVEPYKXSUUGBCGILVFVHWFZXLXNUVEUVAPXMUUMCDGIJVGTWOWPZU
      UHXPUVBXNXPUUHYIUVBYLXLUUHYIUVBPUUGXTCDGIJVGTVIWQXLUVAUVBUVCPUUNUUOCDFGIJ
      NWRTWOUULXRYBUUQXNXPYFUUHYGXAUULUUPYAFUULUUNUUGDRZXTDRZUUPYAUULUVAUUHYIUV
      JUUPSZUVHUUHXNXPWSXPUUHYIXNYLWTXLUVAUUHYIVSUVKPUUNUUGXTCDGIJWAVNWBUULUVIX
      MXTDUUHXNUVIXMSXPUVDUVIXMUUMUUGDRZDRZUUAXMUVDXNUVEUUHUVIUVMSZUVFUVGUUHXNV
      QXLXNUVEUUHVSUVNPXMUUMUUGCDGIJWAVNWBUVDUVLHXMDUUHUVLHSZXNXLUUHUVOPUUGBCDG
      HIJLMWCVNWFWDXNUUDUUHUUEVRWGWPXBXCXDXEUULUUIUURUUJUUSWLUUHXNUUIUURSXPUVDU
      UIUUGXSXMBRDRFUEZUURXLUUHXNUUIUVPSPUUGXMABCDFGIJLNOUTTXLUUHXNUVPUURSPUUGX
      MBCDFGIJLNXFTXGWPUUHXPUUJUUSSZXNXLUUHXPUVQPUUGXOABCDFGIJLNOUTTWQXHXIXKXJ
      $.
      $( [2-Oct-2007] $) $( [29-Nov-2006] $)
  $}

  ${
    imsmet.8 $e |- D = ( IndMet ` U ) $.
    $( The induced metric of a normed complex vector space is a metric space.
       Part of Definition 2.2-1 of [Kreyszig] p. 58. $)
    imsmet $p |- ( U e. NrmCVec -> D e. Met ) $=
      ( cnv wcel cims cfv cme caddc cmul cop cabs cif wceq fveq2 eleq1d cns cpv
      cgn eqid cnm cba cn0v elimnvu imsmetlem dedth syl5eqel ) BDEZABFGZHCUHUIH
      EUHBIJKLKZMZFGZHEBUJBUKNUIULHBUKFOPULUKQGZUKUKRGZUNSGZUKUAGZUKUBGZUKUCGZU
      QTUNTUOTUMTURTUPTULTBUDUEUFUG $.
      $( [2-Oct-2007] $) $( [4-Dec-2006] $)
  $}

  ${
    nvelbl.1 $e |- X = ( BaseSet ` U ) $.
    nvelbl.3 $e |- M = ( -v ` U ) $.
    nvelbl.6 $e |- N = ( norm ` U ) $.
    nvelbl.8 $e |- D = ( IndMet ` U ) $.
    $( Membership of a vector in a ball. $)
    nvelbl $p |- ( ( ( U e. NrmCVec /\ R e. RR+ ) /\ ( P e. X /\ A e. X ) ) ->
                   ( A e. ( P ( ball ` D ) R ) <-> ( N ` ( A M P ) ) < R ) ) $=
      ( wcel wa cfv co clt wbr cdm wceq cnv crp cbl imsba adantr eleq2d anbi12d
      biimpa cme imsmet eqid elbl3 sylanl1 syldan imsdval 3com23 adantlr breq1d
      wb 3expb bitrd ) EUAMZDUBMZNZCHMZAHMZNZNZACDBUCOPMZACBPZDQRZACFPGOZDQRVDV
      GCBSSZMZAVMMZNZVIVKUSZVDVGVPVDVEVNVFVOVDHVMCVBHVMTVCBEHILUDUEZUFVDHVMAVRU
      FUGUHVBBUIMVCVPVQBELUJABCDVMVMUKULUMUNVHVJVLDQVBVGVJVLTZVCVBVEVFVSVBVFVEV
      SACBEFGHIJKLUOUPUTUQURVA $.
      $( [10-Jan-2014] $) $( [27-Dec-2007] $)
  $}

  ${
    nvelbl2.1 $e |- X = ( BaseSet ` U ) $.
    nvelbl2.2 $e |- G = ( +v ` U ) $.
    nvelbl2.6 $e |- N = ( norm ` U ) $.
    nvelbl2.8 $e |- D = ( IndMet ` U ) $.
    $( Membership of an off-center vector in a ball. $)
    nvelbl2 $p |- ( ( ( U e. NrmCVec /\ R e. RR+ ) /\ ( P e. X /\ A e. X ) ) ->
                   ( ( P G A ) e. ( P ( ball ` D ) R ) <-> ( N ` A ) < R ) ) $=
      ( wcel wa co cfv clt wbr 3expb adantlr cnv crp cbl cnsb simprl nvgcl eqid
      wb jca nvelbl syldan wceq nvpncan2 fveq2d breq1d bitrd ) EUAMZDUBMZNZCHMZ
      AHMZNZNZCAFOZCDBUCPOMZVDCEUDPZOZGPZDQRZAGPZDQRUSVBUTVDHMZNVEVIUHVCUTVKUSU
      TVAUEUQVBVKURUQUTVAVKCAEFHIJUFSTUIVDBCDEVFGHIVFUGZKLUJUKVCVHVJDQVCVGAGUQV
      BVGAULZURUQUTVAVMCAEFVFHIJVLUMSTUNUOUP $.
      $( [10-Jan-2014] $) $( [27-Dec-2007] $)
  $}

  ${
    nvlmcl.1 $e |- X = ( BaseSet ` U ) $.
    nvlmcl.2 $e |- D = ( IndMet ` U ) $.
    nvlmcl.3 $e |- P e. _V $.
    $( Closure of the limit of a converging vector sequence. $)
    nvlmcl $p |- ( ( U e. NrmCVec /\ F ( ~~>m ` D ) P ) -> P e. X ) $=
      ( cnv wcel clm cfv wbr wa cdm cme imsmet cvv eqid lmcl mp3an2 sylan imsba
      wceq adantr eleqtrrd ) CIJZDBAKLMZNBAOOZEUGAPJZUHBUIJZACGQUJBRJUHUKHRABDU
      IUISTUAUBUGEUIUDUHACEFGUCUEUF $.
      $( [26-Dec-2007] $)
  $}

  ${
    $d k D $.  $d k F $.  $d k P $.  $d k R $.  $d k U $.  $d k X $.
    nvlmle.1 $e |- X = ( BaseSet ` U ) $.
    nvlmle.6 $e |- N = ( norm ` U ) $.
    nvlmle.8 $e |- D = ( IndMet ` U ) $.
    nvlmle.p $e |- P e. _V $.
    $( If the norm of each member of a converging sequence is less than or
       equal to a given amount, so is the norm of the convergence value. $)
    nvlmle $p |- ( ( ( U e. NrmCVec /\ F : NN --> X /\ F ( ~~>m ` D ) P ) /\
     ( R e. RR /\ A. k e. NN ( N ` ( F ` k ) ) <_ R ) ) -> ( N ` P ) <_ R ) $=
      ( wcel cn cfv wbr w3a cle wa adantr cnv wf cr cv wral cn0v co wceq nvlmcl
      clm eqid nvnd syldan 3adant2 cme cdm imsmet anim1i nvzcl eleqtrd 3ad2ant1
      simprl ffvelrn sylan2 anassrs breq1d biimpd ralimdva imp adantrl 3adantl3
      imsba cvv c1 1z nnuz lmle mp3anl2 syl13anc eqbrtrd ) DUAMZNHFUBZFBAUJOPZQ
      ZCUCMZEUDZFOZGOZCRPZENUEZSZSZBGOZBDUFOZAUGZCRWDWMWOUHZWKWAWCWPWBWAWCBHMWP
      ABDFHIKLUIBADGHWNIWNUKZJKULUMUNTWLAUOMZWCSZWNAUPUPZMZWEWGWNAUGZCRPZENUEZW
      OCRPZWDWSWKWAWCWSWBWAWRWCADKUQURUNTWDXAWKWAWBXAWCWAWNHWTDHWNIWQUSADHIKVLU
      TVATWDWEWJVBWAWBWKXDWCWAWBSZWJXDWEXFWJXDXFWIXCENXFWFNMZSZWIXCXHWHXBCRWAWB
      XGWHXBUHZWBXGSWAWGHMXINHWFFVCWGADGHWNIWQJKULVDVEVFVGVHVIVJVKWRBVMMWCXAWEX
      DQXELVMABWNCEFVNWTNWTUKVOVPVQVRVSVT $.
      $( [25-Dec-2007] $)
  $}

  ${
    $d t u v w x y z $.
    cnims.6 $e |- U = <. <. + , x. >. , abs >. $.
    cnims.7 $e |- D = ( abs o. - ) $.
    $( The metric induced on the complex numbers. ~ cnmet proves that it is a
       metric.  (Contributed by Steve Rodriguez, 5-Dec-2006; revised by NM
       15-Jan-2008.) $)
    cnims $p |- D = ( IndMet ` U ) $=
      ( cabs cmin ccom cims cfv wcel wceq cnnv cnnvm cnnvnm imsval ax-mp eqtr4i
      cnv eqid ) AEFGZBHIZDBRJUATKBCLUABFEBCMBCNUASOPQ $.
      $( [15-Jan-2008] $) $( [28-Nov-2006] $)
  $}

  ${
    vacn.1 $e |- X = ( BaseSet ` U ) $.
    vacn.6 $e |- N = ( norm ` U ) $.
    vacn.8 $e |- C = ( IndMet ` U ) $.
    vacn.9 $e |- U e. NrmCVec $.
    ${
      $d t h u v f g A $.  $d t h u v f g B $.  $d t k u v f g F $.
      $d t k f g u v G $.  $d t k h u v f g H $.  $d t k h u v f g M $.
      $d t u v f g ph $.  $d t k h u v f g X $.  $d t k h u v f g U $.
      $d t k h u v f g C $.
      vacnlem1.1 $e |- F e. _V $.
      vacnlem1.2 $e |- G e. _V $.
      vacnlem1.3 $e |- H e. _V $.
      vacnlem1.4 $e |- A e. _V $.
      vacnlem1.5 $e |- B e. _V $.
      vacnlem1.6 $e |- ( ph <-> ( ( F ( ~~>m ` C ) A /\ G ( ~~>m ` C ) B )
        /\ A. k e. ( ZZ>= ` M ) ( ( F ` k ) e. X /\ ( G ` k ) e. X /\ ( H ` k )
        = ( ( F ` k ) ( +v ` U ) ( G ` k ) ) ) ) ) $.
      $( Lemma for ~ vacn . $)
      vacnlem1 $p |- ( ( t e. ( ZZ>= ` M ) /\ ph ) -> ( ( F ` t ) e. X /\
        ( G ` t ) e. X /\ ( H ` t ) = ( ( F ` t ) ( +v ` U ) ( G ` t ) ) ) ) $=
        ( cv cuz cfv wcel clm wbr wa cpv co wceq w3a wral fveq2 eleq1d opreq12d
        eqeq12d 3anbi123d rcla4va adantrl sylan2b ) ABUDZKUEUFZUGZHCEUHUFZUIIDV
        GUIUJZGUDZHUFZMUGZVIIUFZMUGZVIJUFZVJVLFUKUFZULZUMZUNZGVEUOZUJVDHUFZMUGZ
        VDIUFZMUGZVDJUFZVTWBVOULZUMZUNZUCVFVSWGVHVRWGGVDVEVIVDUMZVKWAVMWCVQWFWH
        VJVTMVIVDHUPZUQWHVLWBMVIVDIUPZUQWHVNWDVPWEVIVDJUPWHVJVTVLWBVOWIWJURUSUT
        VAVBVC $.
        $( [12-Jun-2009] $)

      $( Lemma for ~ vacn . $)
      vacnlem2 $p |- ( ph -> ( A e. X /\ B e. X ) ) $=
        ( clm cfv wbr wa cv wcel cpv co wceq w3a cuz wral nvlmcl anim12i adantr
        cnv mpan sylbi ) AGBDUCUDZUEZHCVAUEZUFZFUGZGUDZLUHVEHUDZLUHVEIUDVFVGEUI
        UDUJUKULFJUMUDUNZUFBLUHZCLUHZUFZUBVDVKVHVBVIVCVJEURUHZVBVIPDBEGLMOTUOUS
        VLVCVJPDCEHLMOUAUOUSUPUQUT $.
        $( [12-Jun-2009] $)

      vacnlem.7 $e |- H C_ ( CC X. X ) $.
      vacnlem.8 $e |- M e. ZZ $.
      $( Lemma for ~ vacn . $)
      vacnlem3 $p |- ( ph -> H ( ~~>m ` C ) ( A ( +v ` U ) B ) ) $=
        ( vv vh vt vu vf vg cpv cfv co wcel cc0 cv clt wbr cle wa cuz wral wrex
        wi clm vacnlem2 cnv eqid nvgcl mp3an1 syl cdiv rehalfcl adantr 2re 2pos
        cr c2 divgt0 mpanr12 jca wceq w3a cme imsmet ax-mp imsbai lmcvg mp3an12
        cvv ex imp anim12i adantlr sylan2 opreq1d breq1d anbi2d adantl ad2antrr
        anbi12d ad3antrrr imsdval syl2anc imbi2d eluzelre ifcl letr exp5o com23
        ancoms com24 mp2d syl2an com34 3ad2ant1 fveq2d caddc nvmcl nvcl sylancr
        readdcl ad2antlr adantrr cc eqcomd 3exp com13 com14 exp31 com3l embantd
        com12 syld cz eluz1i biimpi zre mpd syl5bir anandirs sylanb breq2 fveq2
        reeanv raaanv cif eleq1d rcla4v cnsb simp1d expcom simpld simp2d simprd
        imbi12d vacnlem1 3adant3 eleq1 3ad2ant3 mpbird max2 max1 simp3d simp1lr
        wb simpll simp1ll nvtri simplr lt2add syl22anc 3impib lelttrd nvaddsub4
        recn 2halves 3brtr4d a1i adantlrr imp41 sylibd sylbid ralimdva ad2ant2r
        eqbrtrd zrei mpan2d impcom biimpri 3syl breq1 ralbidv rcla4ev rexlimdvv
        imbi1d sylan exp32 ralrimiv elex cxp wss lmbr2 biimprd mpan sylc ) ABCE
        UKULZUMZLUNZUOUEUPZUQURZUFUPZUGUPZUSURZUXMIULZLUNZUXOUXHDUMZUXJUQURZUTZ
        VDZUGJVAULZVBZUFUYAVCZVDZUEVQVBZIUXHDVEULZURZABLUNZCLUNZUTZUXIABCDEFGHI
        JKLMNOPQRSTUAUBVFZEVGUNZUYHUYIUXIPBCEUXGLMUXGVHZVIVJVKZAUYDUEVQAUXJVQUN
        ZUXKUYCAUYOUXKUTZUTZUHUPZUXMUSURZUXMGULZLUNZUYTBDUMZUXJVRVLUMZUQURZUTZV
        DZUGUYAVBZUHUYAVCZUIUPZUJUPZUSURZVUJHULZLUNZVULCDUMZVUCUQURZUTZVDZUJUYA
        VBZUIUYAVCZUTZUYCUYPAVUCVQUNZUOVUCUQURZUTZVUTUYPVVAVVBUYOVVAUXKUXJVMZVN
        UYPVRVQUNUOVRUQURVVBVOVPUXJVRVSVTWAAGBUYFURZHCUYFURZUTZFUPZGULZLUNVVHHU
        LZLUNVVHIULVVIVVJUXGUMWBWCFUYAVBZUTVVCVUTUBVVGVVCVUTVVKVVEVVFVVCVUTVVEV
        VCUTVUHVVFVVCUTVUSVVEVVCVUHDWDUNZBWJUNZVVEVVCVUHVDUYLVVLPDEOWEWFZTVVLVV
        MVVEWCVVCVUHWJDBVUCUHUGGJLUYADELMOPWGZUDUYAVHZWHWKWIWLVVFVVCVUSVVLCWJUN
        ZVVFVVCVUSVDVVNUAVVLVVQVVFWCVVCVUSWJDCVUCUIUJHJLUYAVVOUDVVPWHWKWIWLWMUU
        AWNUUBWOVUTVUGVURUTZUIUYAVCUHUYAVCUYQUYCVUGVURUHUIUYAUYAUUEUYQVVRUYCUHU
        IUYAUYAUYQUYRUYAUNZVUIUYAUNZUTZVVRUYCVDVVRVUFVUQUTZUJUYAVBZUGUYAVBZUYQV
        WAUTZUYCVUFVUQUGUJUYAUUFVWEVWDUYRVUIUSURZVUIUYRUUGZUXMUSURZUXSVDZUGUYAV
        BZUYCVWEVWCVWIUGUYAVWEUXMUYAUNZUTZVWCVUFVUIUXMUSURZUXMHULZLUNZVWNCDUMZV
        UCUQURZUTZVDZUTZVWIVWKVWCVWTVDVWEVWBVWTUJUXMUYAVUJUXMWBZVUQVWSVUFVXAVUK
        VWMVUPVWRVUJUXMVUIUSUUCVXAVUMVWOVUOVWQVXAVULVWNLVUJUXMHUUDZUUHVXAVUNVWP
        VUCUQVXAVULVWNCDVXBWPWQXAUUPWRUUIWSVWLVWTUYSVUAUYTBEUUJULZUMZKULZVUCUQU
        RZUTZVDZVWMVWOVWNCVXCUMZKULZVUCUQURZUTZVDZUTZVWIVWLVUFVXHVWSVXMVWLVUEVX
        GUYSVWLVUDVXFVUAVWLVUBVXEVUCUQVWLVUAUYHVUBVXEWBZVWEVWKVUAAVWKVUAVDUYPVW
        AVWKAVUAVWKAUTZVUAVWOUXOUYTVWNUXGUMZWBZAUGBCDEFGHIJKLMNOPQRSTUAUBUUQZUU
        KUULWTWLAUYHUYPVWAVWKAUYHUYIUYKUUMZXBUYLVUAUYHVXOPUYTBDEVXCKLMVXCVHZNOX
        CVJXDWQWRXEVWLVWRVXLVWMVWLVWQVXKVWOVWLVWPVXJVUCUQVWLVWOUYIVWPVXJWBZUYQV
        WKVWOVWAAVWKVWOUYPVWKAVWOVXPVUAVWOVXRVXSUUNXKWNWNAUYIUYPVWAVWKAUYHUYIUY
        KUUOZXBUYLVWOUYIVYBPVWNCDEVXCKLMVYANOXCVJXDWQWRXEXAVWLVXNVWHUXPUXOUXHVX
        CUMZKULZUXJUQURZUTZVDVWIVWLVXNVWHVYGVWLVXNUTVWHUTUXPVYFVWLUXPVXNVWHUYQV
        WKUXPVWAAVWKUXPUYPVWKAUXPVXPVUAVWOVXRWCZUXPVXSVYHUXPVXQLUNZVUAVWOVYIVXR
        UYLVUAVWOVYIPUYTVWNEUXGLMUYMVIVJUURVXRVUAUXPVYIUVFVWOUXOVXQLUUSUUTUVAVK
        XKWNWNZWTVWEVWKVXNVWHVYFAUYOVWAVWKVXNVWHVYFVDZVDZVDUXKAUYOUTZVWAUTZVWKV
        YLVXNVYNVWKUTZVYKVXHVXMVYOVYKVDVWHVXMVYOVXHVYFVYOVWHVXMVXHVYFVDZVYOVWHV
        XMVYPVDVYOVWHUTZVWMVXLVYPVYOVWHVWMVWKVYNUXMVQUNZVWHVWMVDZJUXMXFZVYNVYRV
        YSVWAVYRVYSVDZVYMVVSUYRVQUNZVUIVQUNZWUAVVTJUYRXFZJVUIXFZWUBWUCUTZVUIVWG
        USURZVWGVQUNZWUAUYRVUIUVBZWUCWUBWUHVWFVUIUYRVQXGXKZWUCWUGWUHWUAVDVDWUBW
        UCVYRWUHWUGVYSWUCWUHVYRWUGVYSVDWUCWUHVYRWUGVWHVWMVUIVWGUXMXHXIXJXLWSXMX
        NWSWLWOWLVYQVXHVXLVYFVYQUYSVXGVXLVYFVDZVYOVWHUYSVWKVYNVYRVWHUYSVDZVYTVY
        NVYRWULVWAVYRWULVDZVYMVVSWUBWUCWUMVVTWUDWUEWUFWUHUYRVWGUSURZWUMWUJUYRVU
        IUVCWUBWUHWUNWUMVDVDWUCWUBWUHVYRWUNWULWUBWUHVYRWUNVWHUYSUYRVWGUXMXHXIXO
        VNXMXNWSWLWOWLVYOVWHVXGWUKVDZVYMVWKVWHWUOVDVWAVXGVWHVYMVWKUTZWUKVUAVXFV
        WHWUPWUKVDVDVUAWUPVWHVXFWUKVUAWUPVWHVXFWUKVDVDVXLVWHVXFVUAWUPUTZVYFVXLW
        UQVXFVWHVYFVWOVXKWUQVXFVYKVDZVDWUQVWOVXKWURVUAWUPVWOVXKWURVDZVDVUAVWOWU
        PWUSVUAVWOWUPWUSVWHVXKVXFVUAVWOUTZWUPUTZVYFVXKVXFWVAVYFVDVDVDVWHWVAVXFV
        XKVYFWVAVXFVXKVYFWVAVXFVXKWCZVYEVXQUXHVXCUMZKULZUXJUQWVBVYDWVCKWVBUXOVX
        QUXHVXCWVAVXFVXRVXKWUPVXRWUTAVWKVXRUYOVWKAVXRVXPVUAVWOVXRVXSUVDXKWNWSXP
        WPXQWVBVXDVXIUXGUMZKULZVUCVUCXRUMZWVDUXJUQWVBWVFVXEVXJXRUMZWVGWVBUYLWVE
        LUNZWVFVQUNPWVBVXDLUNZVXILUNZWVIWVAVXFWVJVXKWVAVUAUYHWVJVUAVWOWUPUVGWUP
        UYHWUTAUYHUYOVWKVXTWTWSZUYLVUAUYHWVJPUYTBEVXCLMVYAXSVJZXDZXPZWVBVWOUYIW
        VKVUAVWOWUPVXFVXKUVEWVAVXFUYIVXKWUPUYIWUTAUYIUYOVWKVYCWTWSZXPUYLVWOUYIW
        VKPVWNCEVXCLMVYAXSVJZXDZUYLWVJWVKWVIPVXDVXIEUXGLMUYMVIVJXDWVEEKLMNXTYAW
        VBVXEVQUNZVXJVQUNZWVHVQUNWVBUYLWVJWVSPWVBVUAUYHWVJVUAVWOWUPVXFVXKUVHWVA
        VXFUYHVXKWVLXPWVMXDVXDEKLMNXTZYAWVBUYLWVKWVTPWVRVXIEKLMNXTZYAVXEVXJYBXD
        WVBVVAVVAWVGVQUNWVAVXFVVAVXKWUPVVAWUTUYOVVAAVWKVVDYCWSZXPZWWDVUCVUCYBXD
        WVBWVJWVKWVFWVHUSURZWVOWVRUYLWVJWVKWWEPVXDVXIEUXGKLMUYMNUVIVJXDWVAVXFVX
        KWVHWVGUQURZWVAWVSWVTVVAVVAVXFVXKUTWWFVDWVAUYLWVJWVSPWVNWWAYAWVAUYLWVKW
        VTPWVAVWOUYIWVKVUAVWOWUPUVJWVPWVQXDWWBYAWWCWWCVXEVXJVUCVUCUVKUVLUVMUVNW
        VBWVCWVEKWVAVXFWVCWVEWBZVXKWUTVYMWWGVWKWUTAWWGUYOAWUTUYJWWGUYKUYLWUTUYJ
        WWGPUYTVWNBCEUXGVXCLMUYMVYAUVOVJWOYDYDXPXQWVAVXFUXJWVGWBZVXKWUPWWHWUTUY
        OWWHAVWKUYOUXJYEUNZWWHUXJUVPWWIWVGUXJUXJUVQYFVKYCWSXPUVRUWFYGYHUVSYIYJX
        JWLYKWLXLYIWKXLWLYHWNWLYLXJYLWKYKYIWLYMWKUVTUWAWAYJVWLVYGUXSVWHVWLVYFUX
        RUXPVWLVYEUXQUXJUQVWLUXQVYEVWLUXPUXIUXQVYEWBZVYJAUXIUYPVWAVWKUYNXBUYLUX
        PUXIWWJPUXOUXHDEVXCKLMVYANOXCVJXDYFWQWRXEUWBUWCYNUWDVWAVWJUYCVDUYQVWAVW
        JUYCVWAVWGUYAUNZVWJUYCVWAUYRYOUNZJUYRUSURZUTZVUIYOUNZJVUIUSURZUTZUTZVWG
        YOUNZJVWGUSURZUTZWWKVVSWWNVVTWWQVVSWWNJUYRUDYPYQVVTWWQJVUIUDYPYQWMWWRWW
        SWWTWWLWWOWWSWWMWWPWWOWWLWWSVWFVUIUYRYOXGXKUWEWWLWWQWWTWWMWWQWWLWWTWWOW
        WPWWLWWTVDWWOWWLWWPWWTWWOWWLWWPWWTVDZWWOWUCWUBWXBWWLVUIYRUYRYRWUBWUCWXB
        WUFWWPWUGWWTWUIWUFWUHWWPWUGUTWWTVDZWUJWUCWUHWXCVDWUBWUCWUHWXCJVQUNWUCWU
        HWXCJUDUWGJVUIVWGXHVJWKWSYSUWHXKXNWKXJWLUWIWNWAWWKWXAJVWGUDYPUWJUWKUYBV
        WJUFVWGUYAUXLVWGWBZUXTVWIUGUYAWXDUXNVWHUXSUXLVWGUXMUSUWLUWPUWMUWNUWQWKW
        SYNYTWKUWOYTYSUWRUWSUXIUXHWJUNZUYEUYGVDZUXHLUWTIYELUXAUXBZUXIWXEWXFVDVD
        UCWXGUXIUYEWXEUYGWXGUXIUYEWXEUYGVDWXEWXGUXIUYEWCZUYGVVLWXEWXHUYGVDVVNVV
        LWXEUTUYGWXHUEWJDUXHUFUGIJLUYAVVOUDVVPUXCUXDUXEYMYGXOWFYSUXF $.
        $( [12-Jun-2009] $)
    $}

    ${
      $d k U $.  $d k X $.  $d k N $.  $d k C $.  $d k F $.  $d k G $.
      $d k H $.  $d k A $.  $d k B $.  $d k M $.
      vacnlem4.1 $e |- F e. _V $.
      vacnlem4.2 $e |- G e. _V $.
      vacnlem4.3 $e |- A e. _V $.
      vacnlem4.4 $e |- B e. _V $.
      $( Lemma for ~ vacn . $)
      vacnlem4 $p |- ( ( H C_ ( CC X. X ) /\ ( ( M e. ZZ /\ F ( ~~>m ` C ) A
  /\ G ( ~~>m ` C ) B ) /\ A. k e. ( ZZ>= ` M ) ( ( F ` k ) e. X
  /\ ( G ` k ) e. X /\ ( H ` k ) = ( ( F ` k ) ( +v ` U ) ( G ` k ) ) ) ) )
         -> H ( ~~>m ` C ) ( A ( +v ` U ) B ) ) $=
        ( cfv cc cxp wss cz wcel clm wbr w3a cv cpv co wceq wral wa wi c0 fveq1
        cuz eqeq1d 3anbi3d ralbidv anbi2d breq1 imbi12d c1 fveq2 raleqdv imbi1d
        cif cvv iftrue cnex cba fvex eqeltri xpex ssex eqeltrd iffalse syl6eqel
        wn 0ex pm2.61i biid sseq1 0ss elimhyp elimel vacnlem3 dedth exp4c 3imp1
        1z imp ) HUAKUBZUCZIUDUEZFACUFTZUGZGBWRUGZUHZEUIZFTZKUEZXBGTZKUEZXBHTZX
        CXEDUJTZUKZULZUHZEIURTZUMZUNZHABXHUKZWRUGZWPXNXPUOXAXDXFXBWPHUPVIZTZXIU
        LZUHZEXLUMZUNZXQXOWRUGZUOHUPHXQULZXNYBXPYCYDXMYAXAYDXKXTEXLYDXJXSXDXFYD
        XGXRXIXBHXQUQUSUTVAVBHXQXOWRVCVDWQWSWTYAYCWQWSWTYAYCWQWSWTUNZYAUNZYCUOY
        EXTEWQIVEVIZURTZUMZUNZYCUOIVEIYGULZYFYJYCYKYAYIYEYKXTEXLYHIYGURVFVGVBVH
        YJABCDEFGXQYGJKLMNOPQWPXQVJUEWPXQHVJWPHUPVKHWOUAKVLKDVMTVJLDVMVNVOVPVQV
        RWPWAXQUPVJWPHUPVSWBVTWCRSYJWDWPXQWOUCUPWOUCHUPHXQWOWEUPXQWOWEWOWFWGIVE
        UDWMWHWIWJWKWLWJWN $.
        $( [14-Jun-2009] $)
    $}

    ${
      $d x y z h k m q r s t X $.  $d x y z h k m q r U $.
      $d x y z h k m q r t G $.  $d x y z h k m q r t C $.  $d h m q t D $.
      $d x y z h k m q r J $.  $d h k m q r K $.  $d x y z h k m q r N $.
      $d x y z m q t F $.  $d x y z m q t G $.  $d x y z m q s t H $.
      $d x y z m L $.
      vacn.2 $e |- G = ( +v ` U ) $.
      vacn.d $e |- D = { <. <. x , y >. , z >. | ( ( x e. ( X X. X )
         /\ y e. ( X X. X ) ) /\ z = sup ( { ( ( 1st ` x ) C ( 1st ` y ) ) ,
                  ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) } $.
      vacn.j $e |- J = ( MetOpen ` C ) $.
      vacn.k $e |- K = ( MetOpen ` D ) $.
      vacn.f $e |- F
                = { <. k , r >. | ( k e. NN /\ r = ( 1st ` ( h ` k ) ) ) } $.
      vacn.g $e |- L
                = { <. k , r >. | ( k e. NN /\ r = ( 2nd ` ( h ` k ) ) ) } $.
      vacn.h $e |- H
                = { <. k , r >. | ( k e. NN /\ r = ( G ` ( h ` k ) ) ) } $.
      $( Lemma for ~ vacn . $)
      vacnlem5 $p |- ( ( h : NN --> ( X X. X ) /\ h ( ~~>m ` D ) q ) ->
             ( ( F ( ~~>m ` C ) ( 1st ` q ) /\ L ( ~~>m ` C ) ( 2nd ` q ) ) /\
                A. t e. ( ZZ>= ` 1 ) ( ( F ` t ) e. X /\ ( L ` t ) e. X /\
                        ( H ` t ) = ( ( F ` t ) ( +v ` U ) ( L ` t ) ) ) ) ) $=
        ( cn cxp cv wf clm cfv wbr wa c1st c2nd wcel cpv co wceq w3a c1 wral c0
        cuz vex 0ex imsbai cnv cme imsmet ax-mp xplmi2 simpr anim12i syl elnnuz
        ffvelrn adantlr sylan sylan2br adantll cop elxp6 simplbi fveq2d syl6eqr
        df-opr fvex fvopab4 sylbir adantl eqcomi opreqi opreq12d syl5eq 3eqtr4d
        fveq2 3jca ralrimiva jca ) UKQQULZHUMZUNZXGSUMZFUOUPUQZURZJXIUSUPEUOUPZ
        UQZOXIUTUPXLUQZURZDUMZJUPZQVAZXPOUPZQVAZXPLUPZXQXSGVBUPZVCZVDZVEZDVFVIU
        PZVGXKUKQJUNZXMURZUKQOUNZXNURZURZXOABCREEFXIVHIJOXGQQSVJVKEGQTUBUCVLZYL
        GVMVAEVNVAUCEGUBVOVPZYMUEUHUIVQZYHXMYJXNYGXMVRYIXNVRVSVTXKYEDYFXKXPYFVA
        ZURZXRXTYDYOXKXPUKVAZXRXPWAZXKYKYQXRYNYHYQXRYJYGYQXRXMUKQXPJWBWCWCWDWEX
        KYKYOXTYNYJYOXTYHYIYOXTXNYOYIYQXTYRUKQXPOWBWEWCWFWDYPXPXGUPZKUPZYSUSUPZ
        YSUTUPZKVCZYAYCXHYOYTUUCVDXJXHYOURZYTUUAUUBWGZKUPUUCUUDYSUUEKUUDYSXFVAZ
        YSUUEVDZYOXHYQUUFYRUKXFXPXGWBWEUUFUUGUUAQVAUUBQVAURYSQQWHWIVTWJUUAUUBKW
        LWKWCYOYAYTVDZXKYOYQUUHYRIRXPIUMZXGUPZKUPYTUKLUUIXPVDZUUJYSKUUIXPXGXBZW
        JUJYSKWMWNWOWPYOYCUUCVDZXKYOYQUUMYRYQYCXQXSKVCUUCYBKXQXSKYBUDWQWRYQXQUU
        AXSUUBKIRXPUUJUSUPUUAUKJUUKUUJYSUSUULWJUHYSUSWMWNIRXPUUJUTUPUUBUKOUUKUU
        JYSUTUULWJUIYSUTWMWNWSWTWOWPXAXCXDXE $.
        $( [16-Jun-2009] $)

      $( Lemma for ~ vacn . $)
      vacnlem6 $p |- G e. ( K Cn J ) $=
        ( vq vm vs vt ccn co wcel cn cxp cv wf clm cfv wbr wi cme wal wb imsbai
        wral cnv imsmet ax-mp metxp nvgf cdm c1st c2nd cpr cr clt csup dmoprab2
        ltso supex dmeqi dmxpid eqtr2i metcn4 mp3an wa cpv wceq w3a c1 vacnlem5
        cuz cop wss wrel copab relopab releqi mpbir a1i eleq2i biimpi vex eleq1
        fveq2 fveq2d eqeq2d anbi12d eqeq1 anbi2d opelopab sylib ad2antrl simprr
        cc nncn ffvelrn ffvelrni syl adantrr eqeltrd jca opelxpi relssdv adantr
        ex syl56 cz 1z nnex fopabex2 fvex vacnlem4 expcom breq2d mpd cvv df-opr
        mp3anl1 com12 biimpd syl9r fveq1i lmcl elxp6 3syl mp3an12 adantl syl5eq
        simpl sylibrd ralrimiva mpgbir ) JMLUMUNUOZUPPPUQZGURZUSZUUSUIURZEUTVAV
        BZKUVAJVAZDUTVAZVBZVCZUIUURVHVCZGEVDUOZDVDUOZUURPJUSZUUQUVGGVEVFABCDDEP
        PDFPRTUAVGZUVKFVIUOZUVIUADFTVJVKZUVMUCVLZUVMUVLUVJUAFJPRUBVMVKZUIQEDGHJ
        KMLUURPEVNZVNUURUURUQZVNUURUVPUVQABCUURUURAURZVOVABURZVOVADUNUVRVPVAUVS
        VPVADUNVQZVRVSVTEVRUVTVSWBWCUCWAWDUURWEWFZUVKUEUDUHWGWHUUTUVFUIUURUUTUV
        FUVAUURUOZUUTUVBUVEUUTUVBWIZIUVAVOVAZUVDVBZNUVAVPVAZUVDVBZWIUJURZIVAZPU
        OUWHNVAZPUOUWHKVAUWIUWJFWJVAZUNWKWLUJWMWOVAVHZWIZUVEABCUJDEFGHIJKLMNOPQ
        UIRSTUAUBUCUDUEUFUGUHWNUWCUWMKUWDUWFWPZUWKVAZUVDVBZUVEUWCKXRPUQZWQZUWMU
        WPVCUUTUWRUVBUUTUKULKUWQKWRZUUTUWSHURZUPUOZQURZUWTUUSVAZJVAZWKZWIZHQWSZ
        WRUXFHQWTKUXGUHXAXBXCUKURZULURZWPZKUOZUXHUPUOZUXIUXHUUSVAZJVAZWKZWIZUUT
        UXHXRUOZUXIPUOZWIZUXJUWQUOUXKUXJUXGUOZUXPUXKUXTKUXGUXJUHXDXEUXFUXLUXBUX
        NWKZWIUXPHQUXHUXIUKXFULXFUWTUXHWKZUXAUXLUXEUYAUWTUXHUPXGUYBUXDUXNUXBUYB
        UXCUXMJUWTUXHUUSXHXIXJXKUXBUXIWKUYAUXOUXLUXBUXIUXNXLXMXNXOUUTUXPUXSUUTU
        XPWIZUXQUXRUXLUXQUUTUXOUXHXSXPUYCUXIUXNPUUTUXLUXOXQUUTUXLUXNPUOZUXOUUTU
        XLWIUXMUURUOUYDUPUURUXHUUSXTUURPUXMJUVOYAYBYCYDYEYIUXHUXIXRPYFYJYGYHUWR
        UWMKUWDUWFUWKUNZUVDVBZUWCUWPUWMUWRUYFWMYKUOZUWEUWGUWLUWRUYFVCYLUWRUYGUW
        EUWGWLUWLWIUYFUWDUWFDFUJINKWMOPRSTUAHQUPUXCVOVAIYMUFYNHQUPUXCVPVANYMUGY
        NUVAVOYOUVAVPYOYPYQUUBUUCUWCUYFUWPUWCUYEUWOKUVDUYEUWOWKUWCUWDUWFUWKUUAX
        CYRUUDUUEYSUWCUVCUWOKUVDUWCUVCUVAUWKVAUWOUVAJUWKUBUUFUWCUVAUWNUWKUVBUVA
        UWNWKZUUTUVHUVAYTUOZUVBUYHUVNUIXFUVHUYIUVBWLUWBUYHUWDPUOUWFPUOWIZWIZUYH
        YTEUVAUUSUURUWAUUGUWBUYKUVAPPUUHXEUYHUYJUUMUUIUUJUUKXIUULYRUUNYSYIYHUUO
        UUP $.
        $( [11-Jun-2009] $)
    $}
  $}

  ${
    $d h k r x y z U $.
    vacn.11 $e |- X = ( BaseSet ` U ) $.
    vacn.12 $e |- G = ( +v ` U ) $.
    vacn.13 $e |- C = ( IndMet ` U ) $.
    vacn.14 $e |- D = { <. <. x , y >. , z >. | ( ( x e. ( X X. X )
            /\ y e. ( X X. X ) ) /\ z = sup ( { ( ( 1st ` x ) C ( 1st ` y ) ) ,
                ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) } $.
    vacn.15 $e |- J = ( MetOpen ` C ) $.
    vacn.16 $e |- K = ( MetOpen ` D ) $.
    $( Vector addition is continuous.  (Contributed by Jeffrey Hankins,
       16-Jun-2009.) $)
    vacn $p |- ( U e. NrmCVec -> G e. ( K Cn J ) ) $=
      ( wcel cfv wa eqid vh vk vr cnv ccn co caddc cmul cop cabs cif cpv cv cba
      cxp c1st cims c2nd cpr cr clt csup wceq copab2 copn syl5eq xpeq12d eleq2d
      fveq2 anbi12d opreqd preq12d supeq1d eqeq2d oprabbidv opreq12d eleq12d cn
      fveq2d copab cnm elimnvu vacnlem6 dedth ) FUDQZGIHUEUFZQWEFUGUHUIUJUIZUKZ
      ULRZAUMZWHUNRZWKUOZQZBUMZWLQZSZCUMZWJUPRZWNUPRZWHUQRZUFZWJURRZWNURRZWTUFZ
      USZUTVAVBZVCZSZABCVDZVERZWTVERZUEUFZQFWGFWHVCZGWIWFXLXMGFULRWILFWHULVIVFX
      MIXJHXKUEXMIEVERXJPXMEXIVEXMEWJJJUOZQZWNXNQZSZWQWRWSDUFZXBXCDUFZUSZUTVAVB
      ZVCZSZABCVDXINXMYCXHABCXMXQWPYBXGXMXOWMXPWOXMXNWLWJXMJWKJWKXMJFUNRWKKFWHU
      NVIVFZYDVGZVHXMXNWLWNYEVHVJXMYAXFWQXMUTXTXEVAXMXRXAXSXDXMDWTWRWSXMDFUQRWT
      MFWHUQVIVFZVKXMDWTXBXCYFVKVLVMVNVJVOVFVSVFXMHDVERXKOXMDWTVEYFVSVFVPVQABCW
      TXIWHUAUBUBUMZVRQZUCUMZYGUAUMRZUPRVCSUBUCVTZWIYHYIYJWIRVCSUBUCVTZXKXJYHYI
      YJURRVCSUBUCVTZWHWARZWKUCWKTYNTWTTFWBWITXITXKTXJTYKTYMTYLTWCWD $.
      $( [16-Jun-2009] $)
  $}

  ${
    $d f u v w z F $.  $d f z D $.  $d z J $.  $d f u x y z $.
    sqcn.1 $e |- D = ( abs o. - ) $.
    sqcn.j $e |- J = ( MetOpen ` D ) $.
    sqcn.2 $e |- F = ( x e. CC |-> ( x ^ 2 ) ) $.
    $( The square function on complex numbers is continuous. $)
    sqcn $p |- F e. ( J Cn J ) $=
      ( vf vw vv co wcel cn cc cv cfv wceq wa c2 cexp vz vu ccn wf clm copab wi
      wbr wral cme wal cnmet sqcl rgen fmpt mpbi cnmetba eqid metcn4 mp3an cmul
      wb cli c1 cz w3a id ancli elnnuz ffvelrn fveq2 fveq2d fvex fvopab4 adantl
      cuz opreq1 oprex fvmpt sqval eqtrd syl 3jca sylan2br ralrimiva jctil nnex
      1z vex opabex2 climmul syl2an adantrl ad2antrl breqtrrd lmclimnn ffvelrni
      expcom cvv fopab2 sylib sylancr 3imtr4d ancoms mpgbir ) CDDUCKLZMNHOZUDZX
      GUAOZBUEPZUHZIOZMLZJOXLXGPZCPZQRIJUFZXICPZXJUHZUGZUANUIUGZHBUJLZYANNCUDZX
      FXTHUKVBBEULZYCAOZSTKZNLZANUIYBYFANYDUMUNANNYECGUOUPZUAJBBHICXPDDNNBEUQZY
      HFFXPURZUSUTXHXSUANXINLZXHXSYJXHRZXGXIVCUHZXPXQVCUHZXKXRYLYKYMYLYKRXPXIXI
      VAKZXQVCYLXHXPYNVCUHZYJYLYLYLRVDVELZUBOZXGPZNLZYSYQXPPZYRYRVAKZQZVFZUBVDV
      PPZUIZRYOXHYLYLYLVGVHXHUUEYPXHUUCUBUUDYQUUDLXHYQMLZUUCYQVIXHUUFRZYSYSUUBM
      NYQXGVJZUUHUUGYTYRCPZUUAUUFYTUUIQXHIJYQXOUUIMXPXLYQQXNYRCXLYQXGVKVLYIYRCV
      MVNVOUUGYSUUIUUAQUUHYSUUIYRSTKZUUAAYRYEUUJNCYDYRSTVQGYRSTVRVSYRVTWAWBWAWC
      WDWEWHWFXIXIUBXGXGXPVDHWIZUUKIJMXOWGWJUAWIZUULWKWLWMYJXQYNQYLXHYJXQXISTKZ
      YNAXIYEUUMNCYDXISTVQGXISTVRVSXIVTWAWNWOWRNBXIXGEWPXHXRYMVBZYJXHXQWSLMNXPU
      DZUUNXICVMXHXONLZIMUIUUOXHUUPIMXHXMRXNNLUUPMNXLXGVJNNXNCYGWQWBWEIJMNXOXPY
      IWTXAWSBXQXPEWPXBVOXCXDWEXE $.
      $( [10-Jan-2014] $) $( [13-Jun-2007] $)
  $}

  ${
    $d d e x y C $.  $d d e x y D $.  $d d e x y J $.  $d e x y K $.
    $d d e x y N $.  $d e x y U $.
    nmcn.1 $e |- N = ( norm ` U ) $.
    nmcn.2 $e |- C = ( IndMet ` U ) $.
    nmcn.3 $e |- D = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    nmcn.j $e |- J = ( MetOpen ` C ) $.
    nmcn.k $e |- K = ( MetOpen ` D ) $.
    $( The norm of a normed complex vector space is a continuous function.
       (The proof was shortened by Mario Carneiro, 10-Jan-2014.) $)
    nmcn $p |- ( U e. NrmCVec -> N e. ( J Cn K ) ) $=
      ( vx vy vd wcel co cr cfv crp wa ve cnv ccn cdm wf cv clt wbr wi wral cme
      wrex wb imsmet remet eqid remetba metcn sylancl cba nvf imsba feq2d mpbid
      simprr cle w3a eleq2d anbi12d nvcl ex anim12d sylbird metcl mp3an1 3impib
      syl6 syl3an1 cmin cabs cneg cns cpv nvabs wceq remetdval imsdval2 3brtr4d
      c1 syl 3expib jca31 3expa rpre lelttr expdimp an32s syl2an ralrimdva impr
      breq2 imbi1d ralbidv rcla4ev syl2anc ralrimivva mpbir2and ) CUBOZFDEUCPOZ
      AUDUDZQFUEZLUFZMUFZAPZNUFZUGUHZXLFRZXMFRZBPZUAUFZUGUHZUIZMXJUJZNSULZUASUJ
      LXJUJZXHAUKOZBUKOZXIXKYETUMACHUNZBIUOZLUANMABFDEXJQXJUPZJBIUQZKURUSXHCUTR
      ZQFUEXKCFYLYLUPZGVAXHYLXJQFACYLYMHVBZVCVDXHYDLUAXJSXHXLXJOZXTSOZTTYPXNXTU
      GUHZYAUIZMXJUJZYDXHYOYPVEXHYOYPYSXHYOTZYPYRMXJYTXMXJOZTZYPYRUUBXSQOZXNQOZ
      TZXSXNVFUHZTZXTQOZYRYPXHYOUUAUUGXHYOUUAVGUUCUUDUUFXHYOUUAUUCXHYOUUATZXQQO
      ZXRQOZTZUUCXHUUIXLYLOZXMYLOZTZUULXHUUMYOUUNUUAXHYLXJXLYNVHXHYLXJXMYNVHVIZ
      XHUUMUUJUUNUUKXHUUMUUJXLCFYLYMGVJVKXHUUNUUKXMCFYLYMGVJVKVLZVMYGUUJUUKUUCY
      IXQXRBQYKVNVOVQVPXHYFYOUUAUUDYHXLXMAXJYJVNVRXHYOUUAUUFXHUUIUUOUUFUUPXHUUM
      UUNUUFXHUUMUUNVGZXQXRVSPVTRZXLWIWAXMCWBRZPCWCRZPFRXSXNVFXLXMUUTCUVAFYLYMU
      VAUPZUUTUPZGWDUURUULXSUUSWEXHUUMUUNUULUUQVPXQXRBIWFWJXLXMAUUTCUVAFYLYMUVB
      UVCGHWGWHWKVMVPWLWMXTWNUUEUUHUUFYRUUEUUHTUUFYQYAUUCUUDUUHUUFYQTYAUIXSXNXT
      WOWMWPWQWRVKWSWTYCYSNXTSXOXTWEZYBYRMXJUVDXPYQYAXOXTXNUGXAXBXCXDXEXFXG $.
      $( [10-Jan-2014] $) $( [16-May-2007] $)
  $}

  ${
    nmcnc.1 $e |- N = ( norm ` U ) $.
    nmcnc.2 $e |- C = ( IndMet ` U ) $.
    nmcnc.3 $e |- D = ( abs o. - ) $.
    nmcnc.j $e |- J = ( MetOpen ` C ) $.
    nmcnc.k $e |- K = ( MetOpen ` D ) $.
    $( The norm of a normed complex vector space is a continuous function to
       ` CC ` .  (For ` RR ` , see ~ nmcn .) $)
    nmcnc $p |- ( U e. NrmCVec -> N e. ( J Cn K ) ) $=
      ( cnv wcel cme cabs cr ccn co eqid a1i cmin ccom cxp cres w3a wss wa copn
      cfv imsmet remet cnmet 3jca resss sseqtr4i jctir nmcn metcnss sseld sylc
      ) CLMZANMZOUAUBZPPUCZUDZNMZBNMZUEZVEBUFZUGZFDVEUHUIZQRZMFDEQRZMVAVHVIVAVB
      VFVGACHUJVFVAVEVESZUKTVGVABIULTUMVEVCBVCVDUNIUOUPAVECDVKFGHVNJVKSZUQVJVLV
      MFAVEBDVKEJVOKURUSUT $.
      $( [12-Aug-2007] $)
  $}

  ${
    abscn.1 $e |- C = ( abs o. - ) $.
    abscn.2 $e |- R = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    abscn.3 $e |- J = ( MetOpen ` C ) $.
    abscn.4 $e |- K = ( MetOpen ` R ) $.
    $( The absolute value function on complex numbers is continuous.  (The
       proof was shortened by Mario Carneiro, 10-Jan-2014.) $)
    abscn $p |- abs e. ( J Cn K ) $=
      ( caddc cmul cop cabs cnv wcel ccn co eqid cnnv cnnvnm cnims nmcn ax-mp )
      IJKLKZMNLCDOPNUCUCQZRABUCCDLUCUDSAUCUDETFGHUAUB $.
      $( [10-Jan-2014] $) $( [22-Aug-2007] $)
  $}

  $( Absolute value is continuous.  Alternate proof of ~ abscncf . $)
  abscncfALT $p |- abs e. ( CC -cn-> RR ) $=
    ( cabs cmin ccom copn cfv cr cxp cres ccn co ccncf eqid abscn wss wceq ssid
    cc ax-resscn cdm mp2an wrel relco dmcoss subopr fdmi sseqtri eqcomi cncfmet
    relssres eleqtrri ) AABCZDEZUKFFGHZDEZIJZQFKJZUKUMULUNUKLUMLZULLZUNLZMQQNFQ
    NUPUOOQPRQFUKUMULUNUKQQGZHZUKUKUAUKSZUTNVAUKOABUBVBBSUTABUCUTQBUDUEUFUKUTUI
    TUGUQURUSUHTUJ $.
    $( [6-Jun-2008] $)

  ${
    $d r s t u v w A $.  $d r s t u D $.  $d r s t u F $.  $d v w G $.
    $d r s t u J $.  $d r s t u v w X $.
    va1cn.1 $e |- X = ( BaseSet ` U ) $.
    va1cn.2 $e |- G = ( +v ` U ) $.
    va1cn.8 $e |- D = ( IndMet ` U ) $.
    va1cn.j $e |- J = ( MetOpen ` D ) $.
    va1cn.f $e |- F = { <. w , v >. | ( w e. X /\ v = ( w G A ) ) } $.
    va1cn.9 $e |- U e. NrmCVec $.
    ${
      va1cnlem.6 $e |- N = ( norm ` U ) $.
      $( Lemma for ~ va1cn . $)
      va1cnlem $p |- ( A e. X -> F e. ( J Cn J ) ) $=
        ( vu wcel co vr vt vs wf cv clt wbr cfv wi wral crp wrex ccn cnv mp3an1
        nvgcl ancoms ralrimiva fopab2 wa wceq opreq1 fvopab4 opreqan12d adantll
        sylib oprex cnsb nvgrp ax-mp bafval eqid vsfval grpopnpcan2 mpan fveq2d
        w3a 3adant2 3adant1 imsdval syl2anc 3adant3 3eqtr4d 3comr 3expa biimprd
        cgr eqtrd breq1d breq2 imbi1d ralbidv rcla4ev expcom syl ralrimivva cme
        impr wb imsmet imsbai metcn mp2an sylanbrc ) CJSZJJFUDZUAUEZRUEZDTZUBUE
        ZUFUGZXGFUHZXHFUHZDTZUCUEZUFUGZUIZRJUJZUBUKULZUCUKUJUAJUJZFHHUMTSZXEAUE
        ZCGTZJSZAJUJXFXEYDAJYBJSZXEYDEUNSZYEXEYDPYBCEGJKLUPUOUQURABJJYCFOUSVFXE
        XSUAUCJUKXEXGJSZXOUKSZXSXEYGUTZXIXOUFUGZXPUIZRJUJZYHXSUIYIYKRJYIXHJSZUT
        ZXPYJYNXNXIXOUFYNXNXGCGTZXHCGTZDTZXIYGYMXNYQVAXEYGYMXLYOXMYPDABXGYCYOJF
        YBXGCGVBOXGCGVGVCABXHYCYPJFYBXHCGVBOXHCGVGVCVDVEXEYGYMYQXIVAZYGYMXEYRYG
        YMXEVQZYOYPEVHUHZTZIUHZXGXHYTTZIUHZYQXIYSUUAUUCIGWGSZYSUUAUUCVAYFUUEPEG
        LVIVJXGXHCYTGJEGJKLVKEGYTLYTVLZVMVNVOVPYSYOJSZYPJSZYQUUBVAZYGXEUUGYMYFY
        GXEUUGPXGCEGJKLUPUOVRYMXEUUHYGYFYMXEUUHPXHCEGJKLUPUOVSYFUUGUUHUUIPYOYPD
        EYTIJKUUFQMVTUOWAYGYMXIUUDVAZXEYFYGYMUUJPXGXHDEYTIJKUUFQMVTUOWBWCWDWEWH
        WIWFURYHYLXSXRYLUBXOUKXJXOVAZXQYKRJUUKXKYJXPXJXOXIUFWJWKWLWMWNWOWRWPDWQ
        SZUULYAXFXTUTWSYFUULPDEMWTVJZUUMUAUCUBRDDFHHJJDEJKMPXAZNUUNNXBXCXD $.
        $( [12-Aug-2007] $)
    $}

    $( Vector addition is continuous in its first operand. $)
    va1cn $p |- ( A e. X -> F e. ( J Cn J ) ) $=
      ( cnm cfv eqid va1cnlem ) ABCDEFGHEPQZIJKLMNOTRS $.
      $( [12-Aug-2007] $)
  $}

  ${
    $d t u v w A $.  $d r s t u C $.  $d r s t u D $.  $d r s t u F $.
    $d r s t u J $.  $d r s u K $.  $d t u N $.  $d v w S $.
    $d r s t u v w X $.
    sm1cni.1 $e |- X = ( BaseSet ` U ) $.
    sm1cni.4 $e |- S = ( .s ` U ) $.
    sm1cni.7 $e |- C = ( abs o. - ) $.
    sm1cni.8 $e |- D = ( IndMet ` U ) $.
    sm1cni.j $e |- J = ( MetOpen ` C ) $.
    sm1cni.k $e |- K = ( MetOpen ` D ) $.
    sm1cni.f $e |- F = { <. w , v >. | ( w e. CC /\ v = ( w S A ) ) } $.
    sm1cni.9 $e |- U e. NrmCVec $.
    sm1cni.a $e |- A e. X $.
    ${
      sm1cni.2 $e |- G = ( +v ` U ) $.
      sm1cnilem.6 $e |- N = ( norm ` U ) $.
      $( Lemma for ~ sm1cni . $)
      sm1cnilem $p |- F e. ( J Cn K ) $=
        ( vr vu vt vs ccn co wcel cc wf cv clt wbr cfv wi wral crp cme wa cnmet
        wrex wb cnv imsmet ax-mp cnmetba imsbai metcn mp2an nvscl mp3an13 fopab
        cmin cabs cmul cc0 wceq opreq2 breq1d imbi2d ralbidv rexbidv cdiv nvcli
        wne cr elrp mpbiran cle 0re ltleni nvge0 biimpri rpdivcl sylan2 adantll
        bitri subcl abscl syl ad2ant2rl ad2antlr simprl sylibr ltmuldiv syl3anc
        rpre sylib biimprd expr ralrimiv breq2 imbi1d rcla4ev syl2anc 1rp recnd
        c1 mul01 adantlr rpgt0 eqbrtrd ralrimiva sylancr ralimi reximi pm2.61ne
        biidd ax-1 cnmetdval opreq1 oprex fvopab4 opreqan12d imsdval2 mp3an1
        cneg syl2an c1st cvc w3a eqid nvvc vafval smfval bafval vcsubdir mp3an3
        mpan fveq2d 3eqtr2d eqtrd imbi12d ralbidva adantr mpbird rgen2 mpbir2an
        nvs ) HJKUIUJUKZULMHUMZUEUNZUFUNZDUJZUGUNZUOUPZUVEHUQZUVFHUQZEUJZUHUNZU
        OUPZURZUFULUSZUGUTVDZUHUTUSUEULUSZDVAUKEVAUKZUVCUVDUVRVBVEDPVCGVFUKZUVS
        UAEGQVGVHUEUHUGUFDEHJKULMDPVIREGMNQUAVJSVKVLABULMAUNZCFUJZHTUVTUWAULUKC
        MUKZUWBMUKUAUBUWACFGMNOVMVNVOUVQUEUHULUTUVEULUKZUVMUTUKZVBZUVQUVEUVFVPU
        JZVQUQZUVHUOUPZUWHCLUQZVRUJZUVMUOUPZURZUFULUSZUGUTVDZUWFUWOUWIUWHVSVRUJ
        ZUVMUOUPZURZUFULUSZUGUTVDZUWJVSUWJVSVTZUWNUWSUGUTUXAUWMUWRUFULUXAUWLUWQ
        UWIUXAUWKUWPUVMUOUWJVSUWHVRWAWBWCWDWEUWFUWJVSWHZVBZUVMUWJWFUJZUTUKZUWHU
        XDUOUPZUWLURZUFULUSZUWOUWEUXBUXEUWDUXBUWEUWJUTUKZUXEUXIUXBUXIVSUWJUOUPZ
        UXBUXIUWJWIUKZUXJUWJWJZCGLMNUDUAUBWGZWKUXJVSUWJWLUPZUXBVSUWJWMUXMWNUVTU
        WCUXNUAUBCGLMNUDWOVLWKWTZWPUVMUWJWQWRWSUXCUXGUFULUWFUXBUVFULUKZUXGUWFUX
        BUXPVBZVBZUWLUXFUXRUWHWIUKZUVMWIUKZUXKUXJVBZUWLUXFVEUWDUXPUXSUWEUXBUWDU
        XPVBZUWGULUKZUXSUVEUVFXAZUWGXBXCZXDUWEUXTUWDUXQUVMXJXEUXRUXIUYAUXRUXBUX
        IUWFUXBUXPXFUXOXGUXLXKUWHUVMUWJXHXIXLXMXNUWNUXHUGUXDUTUVHUXDVTZUWMUXGUF
        ULUYFUWIUXFUWLUVHUXDUWHUOXOXPWDXQXRUWFUWQUFULUSZUGUTVDZUWTUWFYAUTUKUYGU
        YHXSUWFUWQUFULUWFUXPVBUWPVSUVMUOUWDUXPUWPVSVTZUWEUYBUWHULUKUYIUYBUWHUYE
        XTUWHYBXCYCUWEVSUVMUOUPUWDUXPUVMYDXEYEYFUYGUYGUGYAUTUVHYAVTUYGYKXQYGUYG
        UWSUGUTUWQUWRUFULUWQUWIYLYHYIXCYJUWDUVQUWOVEUWEUWDUVPUWNUGUTUWDUVOUWMUF
        ULUYBUVIUWIUVNUWLUYBUVGUWHUVHUOUVEUVFDPYMWBUYBUVLUWKUVMUOUYBUVLUVECFUJZ
        UVFCFUJZEUJZUWKUWDUXPUVJUYJUVKUYKEABUVEUWBUYJULHUWAUVECFYNTUVECFYOYPABU
        VFUWBUYKULHUWAUVFCFYNTUVFCFYOYPYQUYBUYLUYJYAYTUYKFUJIUJZLUQZUWGCFUJZLUQ
        ZUWKUWDUYJMUKZUYKMUKZUYLUYNVTZUXPUVTUWDUWCUYQUAUBUVECFGMNOVMVNUVTUXPUWC
        UYRUAUBUVFCFGMNOVMVNUVTUYQUYRUYSUAUYJUYKEFGILMNUCOUDQYRYSUUAUYBUYOUYMLU
        WDUXPUWCUYOUYMVTZUBGUUBUQZUUCUKZUWDUXPUWCUUDUYTUVTVUBUAGVUAVUAUUEUUFVHU
        VEUVFCFIVUAMGIUCUUGFGOUUHGIMNUCUUIUUJUULUUKUUMUYBUYCUYPUWKVTZUYDUVTUYCU
        WCVUCUAUBUWGCFGLMNOUDUVBVNXCUUNUUOWBUUPUUQWEUURUUSUUTUVA $.
        $( [10-Jan-2014] $) $( [23-Aug-2007] $)
    $}

    $( Scalar multiplication is continuous in its first operand. $)
    sm1cni $p |- F e. ( J Cn K ) $=
      ( cpv cfv cnm eqid sm1cnilem ) ABCDEFGHGUAUBZIJGUCUBZKLMNOPQRSTUFUDUGUDUE
      $.
      $( [1-Aug-2007] $)
  $}


