$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c NrmCVec $.

  $c +v $.
  $c BaseSet $.
  $c .s $.
  $c 0vec $.
  $c -v $.
  $c norm $.

  $c IndMet $.

  $( Extend class notation with the class of all normed complex vector
     spaces. $)
  cnv $a class NrmCVec $.

  $( Extend class notation with vector addition in a normed complex vector
     space.  In the literature, the subscript "v" is omitted, but we need it to
     avoid ambiguity with complex number addition ` + ` ~ caddc . $)
  cpv $a class +v $.

  $( Extend class notation with the base set of a normed complex vector space.
     (Note that ` BaseSet ` is capitalized because, once it is fixed for a
     particular vector space ` U ` , it is not a function, unlike e.g.
     ` norm ` .  This is our typical convention.) $)
  cba $a class BaseSet $.

  $( Extend class notation with scalar multiplication in a normed complex
     vector space.  In the literature scalar multiplication is usually
     indicated by juxtaposition, but we need an explicit symbol to prevent
     ambiguity. $)
  cns $a class .s $.

  $( Extend class notation with zero vector in a normed complex vector
     space. $)
  cn0v $a class 0vec $.

  $( Extend class notation with vector subtraction in a normed complex vector
     space. $)
  cnsb $a class -v $.

  $( Extend class notation with the norm function in a normed complex vector
     space.  In the literature, the norm of ` A ` is usually written "|| ` A `
     ||", but we use function notation to take advantage of our existing
     theorems about functions. $)
  cnm $a class norm $.

  $( Extend class notation with the class of the induced metrics on normed
     complex vector spaces. $)
  cims $a class IndMet $.

  ${
    $d g s m n o t u w x y z $.  $d y G $.  $d y N $.  $d y S $.
    $( Define the class of all normed complex vector spaces. $)
    df-nv $a |- NrmCVec = { <. <. g , s >. , n >. |
      ( <. g , s >. e. CVec /\ n : ran g --> RR /\ A. x e. ran g
        ( ( ( n ` x ) = 0 -> x = ( Id ` g ) ) /\
          A. y e. CC ( n ` ( y s x ) ) = ( ( abs ` y ) x. ( n ` x ) ) /\
          A. y e. ran g ( n ` ( x g y ) ) <_ ( ( n ` x ) + ( n ` y ) ) ) ) } $.

    $( Structure of the class of all normed complex vectors spaces. $)
    nvss $p |- NrmCVec C_ ( ( _V X. _V ) X. _V ) $=
      ( vg vs vn vx vy cnv cv cop cvc wcel crn cr wf cfv wceq wral w3a cvv cxp
      co cc0 cgi wi cabs cmul cc caddc cle wbr copab2 df-nv oprabss eqsstri )
      FAGZBGZHIJUNKZLCGZMDGZUQNZUAOURUNUBNOUCEGZURUOTUQNUTUDNUSUETOEUFPURUTUNTU
      QNUSUTUQNUGTUHUIEUPPQDUPPQZABCUJRRSRSDEACBUKVAABCULUM $.
      $( [28-Nov-2006] $)

    $( A normed complex vector space is a vector space. $)
    nvvcop $p |- ( <. <. G , S >. , N >. e. NrmCVec -> <. G , S >. e. CVec ) $=
      ( vy vg vs vn vx cop cnv wcel cv c1st cfv cvc wceq co wral wa wex cab crn
      cr wf cc0 cgi wi cabs cmul caddc cle wbr w3a copab2 df-nv simp1 ssoprab2i
      cc copab dfoprab2 pm5.32i 2exbii 19.41vv simprbi ssopab2i eqsstri df-opab
      eleq1 bitr3i fveq2 visset op1st syl6eq eleq1d sstri sseli opex elab sylib
      ss2abi ) BAIZCIZJKWBDLZMNZOKZDUAZKWAOKZJWFWBJELZFLZIZOKZWHUBZUCGLZUDZHLZW
      MNZUEPWOWHUFNPUGWCWOWIQWMNWCUHNWPUIQPDURRWOWCWHQWMNWPWCWMNUJQUKULDWLRUMHW
      LRZUMZEFGUNZWFHDEGFUOWSWKEFGUNZWFWRWKEFGWKWNWQUPUQWTWOOKZHGUSZWFWTWOWJPZW
      KSZFTETZHGUSXBWKEFGHUTXEXAHGXEXCFTETZXAXEXCXASZFTETXFXASXGXDEFXCXAWKWOWJO
      VHVAVBXCXAEFVCVIVDVEVFXBWCWOWMIZPZXASZGTHTZDUAWFXAHGDVGXKWEDXKXIGTHTZWEXK
      XIWESZGTHTXLWESXMXJHGXIWEXAXIWDWOOXIWDXHMNWOWCXHMVJWOWMHVKVLVMVNVAVBXIWEH
      GVCVIVDVTVFVOVOVFVPWEWGDWBWACVQWCWBPZWDWAOXNWDWBMNWAWCWBMVJWACBAVQVLVMVNV
      RVS $.
      $( [5-Jun-2008] $)

    $( Define vector addition on a normed complex vector space. $)
    df-va $a |- +v = ( 1st o. 1st ) $.

    $( Define the base set of a normed complex vector space. $)
    df-ba $a |- BaseSet = ( x e. _V |-> ran ( +v ` x ) ) $.

    $( Define scalar multiplication on a normed complex vector space. $)
    df-sm $a |- .s = ( 2nd o. 1st ) $.

    $( Define the zero vector in a normed complex vector space. $)
    df-0v $a |- 0vec = ( Id o. +v ) $.

    $( Define vector subtraction on a normed complex vector space. $)
    df-vs $a |- -v = ( /g o. +v ) $.

    $( Define the norm function in a normed complex vector space. $)
    df-nm $a |- norm = 2nd $.

    $( Define the induced metric on a normed complex vector space. $)
    df-ims $a |- IndMet = ( u e. NrmCVec |->
                            ( ( norm ` u ) o. ( -v ` u ) ) ) $.
  $}

  $( The class of all normed complex vectors spaces is a relation. $)
  nvrel $p |- Rel NrmCVec $=
    ( cnv wrel cvv cxp wss nvss xpss sstri df-rel mpbir ) ABACCDZEAKCDKFKCGHAIJ
    $.
    $( [14-Nov-2006] $)

  ${
    $d x y U $.
    vafval.2 $e |- G = ( +v ` U ) $.
    $( Value of the function for the vector addition (group) operation on a
       normed complex vector space. $)
    vafval $p |- G = ( 1st ` ( 1st ` U ) ) $=
      ( cpv cfv c1st cvv wcel wceq ccom df-va fveq1i wfun wfo fo1st fofun ax-mp
      wf c0 fvprc fof fvco3 mp3an12 syl5eq wn fveq2d 1st0 syl6req eqtrd pm2.61i
      eqtri ) BADEZAFEZFEZCAGHZULUNIUOULAFFJZEZUNADUPKLFMZGGFRZUOUQUNIGGFNZUROG
      GFPQUTUSOGGFUAQGGAFFUBUCUDUOUEZULSUNADTVAUNSFESVAUMSFAFTUFUGUHUIUJUK $.
      $( [23-Apr-2007] $)
  $}

  ${
    $d u U $.
    bafval.1 $e |- X = ( BaseSet ` U ) $.
    bafval.2 $e |- G = ( +v ` U ) $.
    $( Value of the function for the base set of a normed complex vector
       space. $)
    bafval $p |- X = ran G $=
      ( vu cba cfv cpv crn cvv wcel wceq cv fveq2 rneqd df-ba fvex c0 fvprc rn0
      rnex fvmpt wn eqcomi 3eqtr4a pm2.61i rneqi 3eqtr4i ) AGHZAIHZJZCBJAKLZUJU
      LMFAFNZIHZJULKGUNAMUOUKUNAIOPFQUKAIRUBUCUMUDZSSJZUJULUQSUAUEAGTUPUKSAITPU
      FUGDBUKEUHUI $.
      $( [16-Nov-2013] $) $( [23-Apr-2007] $)
  $}

  ${
    $d u U $.
    smfval.4 $e |- S = ( .s ` U ) $.
    $( Value of the function for the scalar multiplication operation on a
       normed complex vector space. $)
    smfval $p |- S = ( 2nd ` ( 1st ` U ) ) $=
      ( cns cfv c1st c2nd cvv wcel wceq ccom df-sm fveq1i wf wfo fo2nd ax-mp c0
      wfun fvprc fofun fo1st fof fvco3 mp3an12 syl5eq fveq2d 2nd0 syl6req eqtrd
      wn pm2.61i eqtri ) ABDEZBFEZGEZCBHIZUNUPJUQUNBGFKZEZUPBDURLMGSZHHFNZUQUSU
      PJHHGOUTPHHGUAQHHFOVAUBHHFUCQHHBGFUDUEUFUQUKZUNRUPBDTVBUPRGERVBUORGBFTUGU
      HUIUJULUM $.
      $( [24-Apr-2007] $)
  $}

  ${
    $d x y U $.  $d x y z w $.
    0vfval.2 $e |- G = ( +v ` U ) $.
    0vfval.5 $e |- Z = ( 0vec ` U ) $.
    $( Value of the function for the zero vector on a normed complex vector
       space.  (Revised by Mario Carneiro, 21-Dec-2013.) $)
    0vfval $p |- ( U e. V -> Z = ( Id ` G ) ) $=
      ( wcel cvv cgi cfv wceq elisset cpv ccom wfun wfn fngid ax-mp c1st cn0v
      fnfun crn wss wfo fo1st fofn mp3an df-va fneq1i mpbir fvco2 mp3an12 df-0v
      ssv fnco fveq1i eqtri fveq2i 3eqtr4g syl ) ACGAHGZDBIJZKACLVAAIMNZJZAMJZI
      JZDVBIOZMHPZVAVDVFKIHPVGQHIUARVHSSNZHPZSHPZVKSUBZHUCVJHHSUDVKUEHHSUFRZVMV
      LUNHHSSUOUGHMVIUHUIUJHAIMUKULDATJVDFATVCUMUPUQBVEIEURUSUT $.
      $( [21-Dec-2013] $) $( [24-Apr-2007] $)
  $}

  ${
    $d x y U $.
    nmfval.6 $e |- N = ( norm ` U ) $.
    $( Value of the norm function in a normed complex vector space. $)
    nmfval $p |- N = ( 2nd ` U ) $=
      ( cnm cfv c2nd df-nm fveq1i eqtri ) BADEAFECADFGHI $.
      $( [25-Apr-2007] $)
  $}

  ${
    nvop2.1 $e |- W = ( 1st ` U ) $.
    nvop2.6 $e |- N = ( norm ` U ) $.
    $( A normed complex vector space is an ordered pair of a vector space and a
       norm operation. $)
    nvop2 $p |- ( U e. NrmCVec -> U = <. W , N >. ) $=
      ( cnv wcel c1st cfv c2nd cop wrel wceq 1st2nd mpan nmfval opeq12i syl6eqr
      nvrel ) AFGZAAHIZAJIZKZCBKFLTAUCMSAFNOCUABUBDABEPQR $.
      $( [6-Sep-2007] $) $( [28-Nov-2006] $)
  $}

  ${
    nvvop.1 $e |- W = ( 1st ` U ) $.
    nvvop.2 $e |- G = ( +v ` U ) $.
    nvvop.4 $e |- S = ( .s ` U ) $.
    $( The vector space component of a normed complex vector space is an
       ordered pair of the underlying group and a scalar product. $)
    nvvop $p |- ( U e. NrmCVec -> W = <. G , S >. ) $=
      ( cnv wcel c1st cfv c2nd cop cvv cxp wrel wceq cnm fveq2i eqtr4i relxp wa
      eqid nvop2 eleq1d ibi nvss sseli fvex opelxp simpld 1st2nd sylancr vafval
      syl sylib smfval opeq12i syl6eqr ) BHIZDDJKZDLKZMZCAMUTNNOZPDVDIZDVCQNNUA
      UTVEBRKZNIZUTDVFMZVDNOZIZVEVGUBUTVHHIZVJUTVKUTBVHHBVFDEVFUCUDUEUFHVIVHUGU
      HUODVFVDNBRUIUJUPUKDVDULUMCVAAVBCBJKZJKVABCFUNDVLJESTAVLLKVBABGUQDVLLESTU
      RUS $.
      $( [6-Sep-2007] $) $( [28-Nov-2006] $)
  $}

  ${
    $d g n s x y G $.  $d g n s x y N $.  $d g n s x y S $.  $d g n s x y X $.
    $d g n s Z $.
    isnvlem.1 $e |- X = ran G $.
    isnvlem.2 $e |- Z = ( Id ` G ) $.
    $( Lemma for ~ isnv . $)
    isnvlem $p |- ( ( G e. _V /\ S e. _V /\ N e. _V ) ->
                 ( <. <. G , S >. , N >. e. NrmCVec <-> ( <. G , S >. e. CVec
        /\ N : X --> RR /\ A. x e. X ( ( ( N ` x ) = 0 -> x = Z )
           /\ A. y e. CC ( N ` ( y S x ) ) = ( ( abs ` y ) x. ( N ` x ) ) /\
           A. y e. X ( N ` ( x G y ) ) <_ ( ( N ` x ) + ( N ` y ) ) ) ) ) ) $=
      ( wcel cv cvc cr cfv wceq co cc wral w3a cvv vg vs vn cop cnv crn cc0 cgi
      wf wi cabs cmul caddc cle wbr copab2 df-nv eleq2i opeq1 eleq1d rneq feq2d
      syl6eqr fveq2 eqeq2d imbi2d opreq fveq2d breq1d raleqbidv 3anbi123d opeq2
      3anbi13d eqeq1d ralbidv 3anbi2d feq1 fveq1 imbi1d opreq2d eqeq12d breq12d
      opreq12d 3anbi23d eloprabg syl5bb ) DCUDZEUDZUEJWHUAKZUBKZUDZLJZWIUFZMUCK
      ZUIZAKZWNNZUGOZWPWIUHNZOZUJZBKZWPWJPZWNNZXBUKNZWQULPZOZBQRZWPXBWIPZWNNZWQ
      XBWNNZUMPZUNUOZBWMRZSZAWMRZSZUAUBUCUPZJDTJCTJETJSWGLJZFMEUIZWPENZUGOZWPGO
      ZUJZXBWPCPZENZXEYAULPZOZBQRZWPXBDPZENZYAXBENZUMPZUNUOZBFRZSZAFRZSZUEXRWHA
      BUAUCUBUQURXQDWJUDZLJZFMWNUIZWRYCUJZXHYJWNNZXLUNUOZBFRZSZAFRZSXSUUAUUBYEW
      NNZXFOZBQRZUUESZAFRZSYRUAUBUCDCETTTWIDOZWLYTWOUUAXPUUGUUMWKYSLWIDWJUSUTUU
      MWMFMWNUUMWMDUFFWIDVAHVCZVBUUMXOUUFAWMFUUNUUMXAUUBXNUUEXHUUMWTYCWRUUMWSGW
      PUUMWSDUHNGWIDUHVDIVCVEVFUUMXMUUDBWMFUUNUUMXJUUCXLUNUUMXIYJWNWPXBWIDVGVHV
      IVJVMVJVKWJCOZYTXSUUGUULUUAUUOYSWGLWJCDVLUTUUOUUFUUKAFUUOXHUUJUUBUUEUUOXG
      UUIBQUUOXDUUHXFUUOXCYEWNXBWPWJCVGVHVNVOVPVOVMWNEOZUUAXTUULYQXSFMWNEVQUUPU
      UKYPAFUUPUUBYDUUJYIUUEYOUUPWRYBYCUUPWQYAUGWPWNEVRZVNVSUUPUUIYHBQUUPUUHYFX
      FYGYEWNEVRUUPWQYAXEULUUQVTWAVOUUPUUDYNBFUUPUUCYKXLYMUNYJWNEVRUUPWQYAXKYLU
      MUUQXBWNEVRWCWBVOVKVOWDWEWF $.
      $( [7-Jun-2008] $) $( [11-Nov-2006] $)
  $}

  ${
    $d x y G $.  $d x y S $.
    $( The components of a normed complex vector space are sets. $)
    nvex $p |- ( <. <. G , S >. , N >. e. NrmCVec
            -> ( G e. _V /\ S e. _V /\ N e. _V ) ) $=
      ( vx vy cop cnv wcel cvv wa w3a nvvcop syl wn cr df-3an cfv wceq co wral
      cvc vcex crn wf opex jctir sylibr cv cc0 cgi wi cabs cmul cc cle wbr eqid
      caddc isnvlem simp2 syl6bi mpcom wne vcoprne df-ne sylib wfun ffun funopg
      ex adantl syl2im mtod pm2.65i opprc2 eleq1d mtbiri con4i sylanbrc ) BAFZC
      FZGHZBIHZAIHZJZCIHZWCWDWFKWBVTUAHZWEABCLABUBZMWFWBWFNZWBVTVTFZGHZWKBUCZOV
      TUDZWCWDVTIHZKZWKWMWKWEWNJWOWKWEWNWKWGWEABVTLZWHMZBAUEUFWCWDWNPUGWOWKWGWM
      DUHZVTQZUIRWRBUJQZRUKEUHZWRASVTQXAULQWSUMSREUNTWRXABSVTQWSXAVTQURSUOUPEWL
      TKDWLTZKWMDEABVTWLWTWLUQWTUQUSWGWMXBUTVAVBWKWMBARZWKWGXCNZWPWGBAVCXDABVDB
      AVEVFMWKWEWMVTVGZXCWQWLOVTVHWDXEXCUKWCWDXEXCBAIVIVJVKVLVMVNWIWAWJGVTCVOVP
      VQVRWCWDWFPVS $.
      $( [5-Jun-2008] $)
  $}

  ${
    $d x y G $.  $d x y N $.  $d x y S $.  $d x y X $.
    isnv.1 $e |- X = ran G $.
    isnv.2 $e |- Z = ( Id ` G ) $.
    $( The predicate "is a normed complex vector space." $)
    isnv $p |- ( <. <. G , S >. , N >. e. NrmCVec <-> ( <. G , S >. e. CVec
        /\ N : X --> RR /\ A. x e. X ( ( ( N ` x ) = 0 -> x = Z )
           /\ A. y e. CC ( N ` ( y S x ) ) = ( ( abs ` y ) x. ( N ` x ) ) /\
           A. y e. X ( N ` ( x G y ) ) <_ ( ( N ` x ) + ( N ` y ) ) ) ) ) $=
      ( cop wcel cvv w3a cr cv cfv wceq co wral wa cnv cvc wf cc0 wi cabs caddc
      cmul cc cle wbr nvex vcex adantr crn simpld rnexg syl syl5eqel fex sylan2
      ancoms df-3an sylanbrc 3adant3 isnvlem pm5.21nii ) DCJZEJUAKDLKZCLKZELKZM
      ZVHUBKZFNEUCZAOZEPZUDQVOGQUEBOZVOCREPVQUFPVPUHRQBUISVOVQDREPVPVQEPUGRUJUK
      BFSMAFSZMCDEULVMVNVLVRVMVNTVIVJTZVKVLVMVSVNCDUMZUNVNVMVKVMVNFLKVKVMFDUOZL
      HVMVIWALKVMVIVJVTUPDLUQURUSFNLEUTVAVBVIVJVKVCVDVEABCDEFGHIVFVG $.
      $( [5-Jun-2008] $)
  $}

  ${
    $d x y G $.  $d x y N $.  $d x y S $.  $d x y X $.
    isnvi.5 $e |- X = ran G $.
    isnvi.6 $e |- Z = ( Id ` G ) $.
    isnvi.7 $e |- <. G , S >. e. CVec $.
    isnvi.8 $e |- N : X --> RR $.
    isnvi.9 $e |- ( ( x e. X /\ ( N ` x ) = 0 ) -> x = Z ) $.
    isnvi.10 $e |- ( ( y e. CC /\ x e. X ) ->
                    ( N ` ( y S x ) ) = ( ( abs ` y ) x. ( N ` x ) ) ) $.
    isnvi.11 $e |- ( ( x e. X /\ y e. X ) ->
                    ( N ` ( x G y ) ) <_ ( ( N ` x ) + ( N ` y ) ) ) $.
    isnvi.12 $e |- U = <. <. G , S >. , N >. $.
    $( Properties that determine a normed complex vector space. $)
    isnvi $p |- U e. NrmCVec $=
      ( wcel cfv wceq co cop cnv cvc cr wf cv cc0 wi cabs cmul cc caddc cle wbr
      wral w3a isnv ex ancoms r19.21aiva 3jca rgen mpbir3an eqeltri ) DECUAZFUA
      ZUBPVFUBQVEUCQGUDFUEAUFZFRZUGSZVGHSZUHZBUFZVGCTFRVLUIRVHUJTSZBUKUOZVGVLET
      FRVHVLFRULTUMUNZBGUOZUPZAGUOABCEFGHIJUQKLVQAGVGGQZVKVNVPVRVIVJMURVRVMBUKV
      LUKQVRVMNUSUTVRVOBGOUTVAVBVCVD $.
      $( [10-Jun-2008] $) $( [15-Apr-2007] $)
  $}

  ${
    $d x y G $.  $d x y N $.  $d x U $.  $d x y S $.  $d x y X $.
    nvi.1 $e |- X = ( BaseSet ` U ) $.
    nvi.2 $e |- G = ( +v ` U ) $.
    nvi.4 $e |- S = ( .s ` U ) $.
    nvi.5 $e |- Z = ( 0vec ` U ) $.
    nvi.6 $e |- N = ( norm ` U ) $.
    $( The properties of a normed complex vector space, which is a vector space
       accompanied by a norm. $)
    nvi $p |- ( U e. NrmCVec -> ( <. G , S >. e. CVec /\ N : X --> RR /\
       A. x e. X ( ( ( N ` x ) = 0 -> x = Z )
           /\ A. y e. CC ( N ` ( y S x ) ) = ( ( abs ` y ) x. ( N ` x ) ) /\
              A. y e. X ( N ` ( x G y ) ) <_ ( ( N ` x ) + ( N ` y ) ) ) ) ) $=
      ( cnv wcel cfv wceq co wral w3a cop cvc cr wf cv cc0 wi cabs cc caddc cle
      cmul wbr c1st eqid nvop2 nvvop opeq1d eqtrd id eqeltrrd bafval isnv sylib
      cgi 0vfval eqeq2d imbi2d 3anbi1d ralbidv 3anbi3d mpbird ) DNOZECUAZUBOZGU
      CFUDZAUEZFPZUFQZVQHQZUGZBUEZVQCRFPWBUHPVRULRQBUISZVQWBERFPVRWBFPUJRUKUMBG
      SZTZAGSZTVOVPVSVQEVEPZQZUGZWCWDTZAGSZTZVMVNFUAZNOWLVMDWMNVMDDUNPZFUAWMDFW
      NWNUOZMUPVMWNVNFCDEWNWOJKUQURUSVMUTVAABCEFGWGDEGIJVBWGUOVCVDVMWFWKVOVPVMW
      EWJAGVMWAWIWCWDVMVTWHVSVMHWGVQDENHJLVFVGVHVIVJVKVL $.
      $( [21-Dec-2013] $) $( [11-Nov-2006] $)
  $}

  ${
    $d x y U $.  $d x y W $.
    nvvc.1 $e |- W = ( 1st ` U ) $.
    $( The vector space component of a normed complex vector space. $)
    nvvc $p |- ( U e. NrmCVec -> W e. CVec ) $=
      ( vx vy cnv wcel cpv cfv cns cop cvc eqid nvvop cba cr cv wceq co wral wf
      cnm cc0 cn0v wi cabs cmul cc caddc cle wbr w3a nvi simp1d eqeltrd ) AFGZB
      AHIZAJIZKZLURAUQBCUQMZURMZNUPUSLGAOIZPAUBIZUADQZVCIZUCRVDAUDIZRUEEQZVDURS
      VCIVGUFIVEUGSREUHTVDVGUQSVCIVEVGVCIUISUJUKEVBTULDVBTDEURAUQVCVBVFVBMUTVAV
      FMVCMUMUNUO $.
      $( [21-Dec-2013] $) $( [28-Nov-2006] $)
  $}

  ${
    nvabl.1 $e |- G = ( +v ` U ) $.
    $( The vector addition operation of a normed complex vector space is an
       Abelian group. $)
    nvabl $p |- ( U e. NrmCVec -> G e. AbelOp ) $=
      ( cnv wcel c1st cfv cvc cabl eqid nvvc vafval vcabl syl ) ADEAFGZHEBIEAOO
      JKBOABCLMN $.
      $( [15-Feb-2008] $)

    $( The vector addition operation of a normed complex vector space is a
       group. $)
    nvgrp $p |- ( U e. NrmCVec -> G e. GrpOp ) $=
      ( cnv wcel cabl cgr nvabl ablogrpo syl ) ADEBFEBGEABCHBIJ $.
      $( [15-Feb-2008] $)
  $}

  ${
    nvgf.1 $e |- X = ( BaseSet ` U ) $.
    nvgf.2 $e |- G = ( +v ` U ) $.
    $( Mapping for the vector addition operation. $)
    nvgf $p |- ( U e. NrmCVec -> G : ( X X. X ) --> X ) $=
      ( cnv wcel cgr cxp wfo wf nvgrp bafval grpfo fof 3syl ) AFGBHGCCIZCBJQCBK
      ABELBCABCDEMNQCBOP $.
      $( [28-Jan-2008] $)
  $}

  ${
    nvsf.1 $e |- X = ( BaseSet ` U ) $.
    nvsf.4 $e |- S = ( .s ` U ) $.
    $( Mapping for the scalar multiplication operation. $)
    nvsf $p |- ( U e. NrmCVec -> S : ( CC X. X ) --> X ) $=
      ( cnv wcel c1st cfv cvc cc cxp wf eqid nvvc cpv vafval smfval bafval vcsm
      syl ) BFGBHIZJGKCLCAMBUBUBNOABPIZUBCBUCUCNZQABERBUCCDUDSTUA $.
      $( [28-Jan-2008] $)
  $}

  ${
    nvgcl.1 $e |- X = ( BaseSet ` U ) $.
    nvgcl.2 $e |- G = ( +v ` U ) $.
    $( Closure law for the vector addition (group) operation of a normed
       complex vector space. $)
    nvgcl $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                    ( A G B ) e. X ) $=
      ( cnv wcel cgr co nvgrp bafval grpocl syl3an1 ) CHIDJIAEIBEIABDKEICDGLABD
      ECDEFGMNO $.
      $( [23-Apr-2007] $)

    $( The vector addition (group) operation is commutative. $)
    nvcom $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( A G B ) = ( B G A ) ) $=
      ( cnv wcel cabl co wceq nvabl bafval ablocom syl3an1 ) CHIDJIAEIBEIABDKBA
      DKLCDGMABDECDEFGNOP $.
      $( [4-Dec-2007] $)

    $( The vector addition (group) operation is associative. $)
    nvass $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
              ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( cnv wcel cgr w3a co wceq nvgrp bafval grpoass sylan ) DIJEKJAFJBFJCFJLA
      BEMCEMABCEMEMNDEHOABCEFDEFGHPQR $.
      $( [4-Dec-2007] $)

    $( Commutative/associative law for vector addition. $)
    nvadd12 $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( A G ( B G C ) ) = ( B G ( A G C ) ) ) $=
      ( cnv wcel w3a wa co wceq nvcom 3adant3r3 opreq1d nvass 3ancoma sylan2b
      3eqtr3d ) DIJZAFJZBFJZCFJZKZLZABEMZCEMBAEMZCEMZABCEMEMBACEMEMZUGUHUICEUBU
      CUDUHUINUEABDEFGHOPQABCDEFGHRUFUBUDUCUEKUJUKNUCUDUESBACDEFGHRTUA $.
      $( [14-Dec-2007] $)

    $( Commutative/associative law for vector addition. $)
    nvadd23 $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cnv wcel cabl w3a co wceq nvabl bafval abl23 sylan ) DIJEKJAFJBFJCFJLAB
      EMCEMACEMBEMNDEHOABCEFDEFGHPQR $.
      $( [27-Dec-2007] $)

    $( Right cancellation law for vector addition. $)
    nvrcan $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( A G C ) = ( B G C ) <-> A = B ) ) $=
      ( cnv wcel cgr w3a co wceq wb nvgrp bafval grporcan sylan ) DIJEKJAFJBFJC
      FJLACEMBCEMNABNODEHPABCEFDEFGHQRS $.
      $( [4-Dec-2007] $)

    $( Left cancellation law for vector addition. $)
    nvlcan $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( C G A ) = ( C G B ) <-> A = B ) ) $=
      ( cnv wcel cgr w3a co wceq wb nvgrp bafval grpolcan sylan ) DIJEKJAFJBFJC
      FJLCAEMCBEMNABNODEHPABCEFDEFGHQRS $.
      $( [14-Dec-2007] $)

    $( Rearrangement of 4 terms in a vector sum. $)
    nvadd4 $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X ) /\ ( C e. X /\
       D e. X ) ) -> ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( cnv wcel cabl wa co wceq nvabl bafval abl4 syl3an1 ) EJKFLKAGKBGKMCGKDG
      KMABFNCDFNFNACFNBDFNFNOEFIPABCDFGEFGHIQRS $.
      $( [8-Feb-2008] $)
  $}

  ${
    nvscl.1 $e |- X = ( BaseSet ` U ) $.
    nvscl.4 $e |- S = ( .s ` U ) $.
    $( Closure law for the scalar product operation of a normed complex vector
       space. $)
    nvscl $p |- ( ( U e. NrmCVec /\ A e. CC /\ B e. X ) ->
                       ( A S B ) e. X ) $=
      ( cnv wcel c1st cfv cvc cc co eqid nvvc cpv vafval smfval bafval syl3an1
      vccl ) DHIDJKZLIAMIBEIABCNEIDUCUCOPABCDQKZUCEDUDUDOZRCDGSDUDEFUETUBUA $.
      $( [1-Feb-2007] $)

    $( Identity element for the scalar product of a normed complex vector
       space. $)
    nvsid $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( 1 S A ) = A ) $=
      ( cnv wcel c1st cfv cvc c1 co wceq eqid nvvc cpv vafval smfval bafval
      vcid sylan ) CGHCIJZKHADHLABMANCUCUCOPABCQJZUCDCUDUDOZRBCFSCUDDEUETUAUB
      $.
      $( [4-Dec-2007] $)

    $( Associative law for the scalar product of a normed complex vector
       space. $)
    nvsass $p |- ( ( U e. NrmCVec /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
      ( ( A x. B ) S C ) = ( A S ( B S C ) ) ) $=
      ( cnv wcel c1st cfv cvc cc w3a cmul co wceq eqid nvvc vafval smfval vcass
      cpv bafval sylan ) EIJEKLZMJANJBNJCFJOABPQCDQABCDQDQREUGUGSTABCDEUDLZUGFE
      UHUHSZUADEHUBEUHFGUIUEUCUF $.
      $( [17-Nov-2007] $)

    $( Commutative law for the scalar product of a normed complex vector
       space. $)
    nvscom $p |- ( ( U e. NrmCVec /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
      ( A S ( B S C ) ) = ( B S ( A S C ) ) ) $=
      ( cnv wcel cc w3a wa cmul co wceq mulcom opreq1d 3adant3 nvsass 3ancoma
      adantl sylan2b 3eqtr3d ) EIJZAKJZBKJZCFJZLZMABNOZCDOZBANOZCDOZABCDODOBACD
      ODOZUIUKUMPZUEUFUGUOUHUFUGMUJULCDABQRSUBABCDEFGHTUIUEUGUFUHLUMUNPUFUGUHUA
      BACDEFGHTUCUD $.
      $( [14-Feb-2008] $)
  $}

  ${
    nvdi.1 $e |- X = ( BaseSet ` U ) $.
    nvdi.2 $e |- G = ( +v ` U ) $.
    nvdi.4 $e |- S = ( .s ` U ) $.
    $( Distributive law for the scalar product of a complex vector space. $)
    nvdi $p |- ( ( U e. NrmCVec /\ ( A e. CC /\ B e. X /\ C e. X ) ) ->
       ( A S ( B G C ) ) = ( ( A S B ) G ( A S C ) ) ) $=
      ( cnv wcel c1st cfv cvc cc w3a co wceq eqid nvvc vafval smfval vcdi sylan
      bafval ) EKLEMNZOLAPLBGLCGLQABCFRDRABDRACDRFRSEUGUGTUAABCDFUGGEFIUBDEJUCE
      FGHIUFUDUE $.
      $( [4-Dec-2007] $)

    $( Distributive law for the scalar product of a complex vector space. $)
    nvdir $p |- ( ( U e. NrmCVec /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
      ( ( A + B ) S C ) = ( ( A S C ) G ( B S C ) ) ) $=
      ( cnv wcel c1st cfv cvc cc w3a caddc co wceq eqid nvvc vafval vcdir sylan
      smfval bafval ) EKLEMNZOLAPLBPLCGLQABRSCDSACDSBCDSFSTEUHUHUAUBABCDFUHGEFI
      UCDEJUFEFGHIUGUDUE $.
      $( [4-Dec-2007] $)

    $( A vector plus itself is two times the vector. $)
    nv2 $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( A G A ) = ( 2 S A ) ) $=
      ( cnv wcel c1st cfv cvc co c2 wceq eqid nvvc vafval smfval bafval sylan
      vc2 ) CIJCKLZMJAEJAADNOABNPCUDUDQRABDUDECDGSBCHTCDEFGUAUCUB $.
      $( [9-Feb-2008] $)
  $}

  ${
    $d f g x y z $.
    vsfval.2 $e |- G = ( +v ` U ) $.
    vsfval.3 $e |- M = ( -v ` U ) $.
    $( Value of the function for the vector subtraction operation on a normed
       complex vector space. $)
    vsfval $p |- M = ( /g ` G ) $=
      ( vx vy vg cfv cpv cgs cvv wcel wceq cdm c1st ax-mp wfun cgr c0 cnsb ccom
      df-vs fveq1i df-va dmeqi crn wfo fo1st fof fdmi forn eqtr4i dmcoeq 3eqtri
      wf eleq2i cv cgn cmpt2 wral visset rnex mpt2ex a1i rgen df-gdiv fmpt mpbi
      co ffun fofun funco mp2an wb funeq mpbir fvco mp3an12 sylbir syl5eq 0ngrp
      wn mtbir ndmfv fvprc fveq2d 3eqtr4rd pm2.61i fveq2i 3eqtr4i ) AUAIZAJIZKI
      ZCBKIALMZWLWNNWOWLAKJUBZIZWNAUAWPUCUDWOAJOZMZWQWNNZWRLAWRPPUBZOZPOZLJXAUE
      UFXCPUGZNXBXCNXCLXDLLPLLPUHZLLPUPUILLPUJQUKZXEXDLNUILLPULQUMPPUNQXFUOUQKR
      ZJRZWSWTSLKUPZXGFGHURZUGZXKFURGURXJUSIIXJVJZUTZLMZHSVAXIXNHSXNXJSMFGXKXKX
      LXJHVBVCZXOVDVEVFHSLXMKFGHVGVHVIZSLKVKQXHXARZPRZXRXQXEXRUILLPVLQZXSPPVMVN
      JXANXHXQVOUEJXAVPQVQAKJVRVSVTWAWOWCZTKIZTWNWLYATNZXTTKOZMZWCYBYDTSMWBYCST
      SLKXPUKUQWDTKWEQVEXTWMTKAJWFWGAUAWFWHWIEBWMKDWJWK $.
      $( [21-Dec-2013] $) $( [15-Feb-2008] $)
  $}

  ${
    nvzcl.1 $e |- X = ( BaseSet ` U ) $.
    nvzcl.6 $e |- Z = ( 0vec ` U ) $.
    $( Closure law for the zero vector of a normed complex vector space. $)
    nvzcl $p |- ( U e. NrmCVec -> Z e. X ) $=
      ( cnv wcel cpv cfv cgi eqid 0vfval cgr nvgrp bafval grpoidcl syl eqeltrd
      ) AFGZCAHIZJIZBATFCTKZELSTMGUABGATUBNUATBATBDUBOUAKPQR $.
      $( [21-Dec-2013] $) $( [27-Nov-2007] $)
  $}

  ${
    nv0id.1 $e |- X = ( BaseSet ` U ) $.
    nv0id.2 $e |- G = ( +v ` U ) $.
    nv0id.6 $e |- Z = ( 0vec ` U ) $.
    $( The zero vector is a right identity element. $)
    nv0rid $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( A G Z ) = A ) $=
      ( cnv wcel wa co cgi cfv wceq 0vfval opreq2d adantr cgr nvgrp bafval eqid
      grporid sylan eqtrd ) BIJZADJZKAECLZACMNZCLZAUFUHUJOUGUFEUIACBCIEGHPQRUFC
      SJUGUJAOBCGTAUICDBCDFGUAUIUBUCUDUE $.
      $( [21-Dec-2013] $) $( [28-Nov-2007] $)

    $( The zero vector is a left identity element. $)
    nv0lid $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( Z G A ) = A ) $=
      ( cnv wcel wa co cgi cfv wceq 0vfval opreq1d adantr cgr nvgrp bafval eqid
      grpolid sylan eqtrd ) BIJZADJZKEACLZCMNZACLZAUFUHUJOUGUFEUIACBCIEGHPQRUFC
      SJUGUJAOBCGTAUICDBCDFGUAUIUBUCUDUE $.
      $( [21-Dec-2013] $) $( [28-Nov-2007] $)
  $}

  ${
    nv0.1 $e |- X = ( BaseSet ` U ) $.
    nv0.4 $e |- S = ( .s ` U ) $.
    nv0.6 $e |- Z = ( 0vec ` U ) $.
    $( Zero times a vector is the zero vector. $)
    nv0 $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( 0 S A ) = Z ) $=
      ( cnv wcel wa cc0 co cpv cfv cgi c1st cvc wceq eqid nvvc vafval vc0 sylan
      smfval bafval 0vfval adantr eqtr4d ) CIJZADJZKLABMZCNOZPOZEUJCQOZRJUKULUN
      SCUOUOTUAABUMUODUNCUMUMTZUBBCGUECUMDFUPUFUNTUCUDUJEUNSUKCUMIEUPHUGUHUI $.
      $( [21-Dec-2013] $) $( [27-Nov-2007] $)
  $}

  ${
    nvsz.4 $e |- S = ( .s ` U ) $.
    nvsz.6 $e |- Z = ( 0vec ` U ) $.
    $( Anything times the zero vector is the zero vector. $)
    nvsz $p |- ( ( U e. NrmCVec /\ A e. CC ) -> ( A S Z ) = Z ) $=
      ( cnv wcel cc wa cpv cfv cgi co c1st cvc wceq eqid nvvc cba vafval smfval
      bafval vcz sylan 0vfval adantr opreq2d 3eqtr4d ) CGHZAIHZJZACKLZMLZBNZUNA
      DBNDUJCOLZPHUKUOUNQCUPUPRSABUMUPCTLZUNCUMUMRZUABCEUBCUMUQUQRURUCUNRUDUEUL
      DUNABUJDUNQUKCUMGDURFUFUGZUHUSUI $.
      $( [21-Dec-2013] $) $( [28-Nov-2007] $)
  $}

  ${
    nvinv.1 $e |- X = ( BaseSet ` U ) $.
    nvinv.2 $e |- G = ( +v ` U ) $.
    nvinv.4 $e |- S = ( .s ` U ) $.
    nvinv.5 $e |- M = ( inv ` G ) $.
    $( Minus 1 times a vector is the underlying group's inverse element.
       Equation 2 of [Kreyszig] p. 51. $)
    nvinv $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( -u 1 S A ) = ( M ` A ) ) $=
      ( cnv wcel c1st cfv cvc c1 cneg co wceq eqid nvvc vafval smfval vcm sylan
      bafval ) CKLCMNZOLAFLPQABRAENSCUGUGTUAABDEUGFCDHUBBCIUCCDFGHUFJUDUE $.
      $( [15-Feb-2008] $)
  $}

  ${
    $d x G $.  $d x N $.  $d x U $.
    invfval.2 $e |- G = ( +v ` U ) $.
    invfval.4 $e |- S = ( .s ` U ) $.
    invfval.3 $e |- N = ( S o. `' ( 2nd |` ( { -u 1 } X. _V ) ) ) $.
    $( Function for the negative of a vector on a normed complex vector space,
       in terms of the underlying addition group inverse.  (We currently do not
       have a separate notation for the negative of a vector.) $)
    invfval $p |- ( U e. NrmCVec -> N = ( inv ` G ) ) $=
      ( vx cnv wcel cgn cfv wceq c1 cc wfn wf eqid ffn syl cv cba wral cneg cxp
      wa co nvsf ax-1cn negcli curry1val mp3an2 sylan nvinv eqtrd r19.21aiva wb
      curry1f sylancl wf1o nvgrp bafval grpinvf f1ofn eqfnfv2 syl11anc mpbird
      cgr ) BIJZDCKLZMZHUAZDLZVLVJLZMZHBUBLZUCZVIVOHVPVIVLVPJZUFVMNUDZVLAUGZVNV
      IAOVPUEZPZVRVMVTMZVIWAVPAQZWBABVPVPRZFUHZWAVPASTWBVSOJZVRWCNUIUJZOVPVSVLV
      PADGUKULUMVLABCVJVPWEEFVJRZUNUOUPVIDVPPZVJVPPZVKVQUQVIVPVPDQZWJVIWDWGWLWF
      WHOVPVSVPADGURUSVPVPDSTVIVPVPVJUTZWKVICVHJWMBCEVACVJVPBCVPWEEVBWIVCTVPVPV
      JVDTHVPDVJVEVFVG $.
      $( [27-Mar-2008] $)
  $}

  ${
    nvm.1 $e |- X = ( BaseSet ` U ) $.
    nvm.2 $e |- G = ( +v ` U ) $.
    nvm.3 $e |- M = ( -v ` U ) $.
    nvm.6 $e |- N = ( /g ` G ) $.
    $( Vector subtraction in terms of group division operation. $)
    nvm $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
          ( A M B ) = ( A N B ) ) $=
      ( co wceq cnv wcel w3a cgs cfv vsfval eqtr4i opreqi a1i ) ABELABFLMCNOAGO
      BGOPEFABEDQRFCDEIJSKTUAUB $.
      $( [15-Feb-2008] $)
  $}

  ${
    $d x y G $.  $d x y U $.  $d x y X $.
    nvmval.1 $e |- X = ( BaseSet ` U ) $.
    nvmval.2 $e |- G = ( +v ` U ) $.
    nvmval.4 $e |- S = ( .s ` U ) $.
    nvmval.3 $e |- M = ( -v ` U ) $.
    $( Value of vector subtraction on a normed complex vector space. $)
    nvmval $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( A M B ) = ( A G ( -u 1 S B ) ) ) $=
      ( cnv wcel w3a cgs cfv co cgn wceq eqid cneg cgr bafval grpdivval syl3an1
      c1 nvgrp nvm nvinv 3adant2 opreq2d 3eqtr4d ) DLMZAGMZBGMZNZABEOPZQZABERPZ
      PZEQZABFQAUFUABCQZEQUMEUBMUNUOURVASDEIUGABUQEUSGDEGHIUCUSTZUQTZUDUEABDEFU
      QGHIKVDUHUPVBUTAEUMUOVBUTSUNBCDEUSGHIJVCUIUJUKUL $.
      $( [11-Sep-2007] $)

    $( Value of vector subtraction on a normed complex vector space. $)
    nvmval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                ( A M B ) = ( ( -u 1 S B ) G A ) ) $=
      ( cnv wcel w3a co c1 cneg nvmval wceq simp1 simp2 cc ax-1cn negcli mp3an2
      nvscl 3adant2 nvcom syl111anc eqtrd ) DLMZAGMZBGMZNZABFOAPQZBCOZEOZUPAEOZ
      ABCDEFGHIJKRUNUKULUPGMZUQURSUKULUMTUKULUMUAUKUMUSULUKUOUBMUMUSPUCUDUOBCDG
      HJUFUEUGAUPDEGHIUHUIUJ $.
      $( [19-Nov-2013] $)

    $( Value of the function for the vector subtraction operation on a normed
       complex vector space.  (Revised by Mario Carneiro, 23-Dec-2013.) $)
    nvmfval $p |- ( U e. NrmCVec -> M = ( x e. X , y e. X |->
                    ( x G ( -u 1 S y ) ) ) ) $=
      ( cnv wcel cv cgn cfv co cmpt2 c1 wceq cgr nvgrp bafval vsfval grpdivfval
      cneg eqid syl w3a nvinv 3adant2 opreq2d mpt2eq3dva eqtr4d ) DLMZFABGGANZB
      NZEOPZPZEQZRZABGGUPSUFUQCQZEQZRUOEUAMFVATDEIUBABFEURGDEGHIUCURUGZDEFIKUDU
      EUHUOABGGVCUTUOUPGMZUQGMZUIVBUSUPEUOVFVBUSTVEUQCDEURGHIJVDUJUKULUMUN $.
      $( [23-Dec-2013] $) $( [11-Sep-2007] $)
  $}

  ${
    nvzs.1 $e |- X = ( BaseSet ` U ) $.
    nvzs.3 $e |- M = ( -v ` U ) $.
    nvzs.4 $e |- S = ( .s ` U ) $.
    nvzs.6 $e |- Z = ( 0vec ` U ) $.
    $( Two ways to express the negative of a vector. $)
    nvzs $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( Z M A ) = ( -u 1 S A ) ) $=
      ( cnv wcel wa co c1 cneg cpv cfv wceq simpl nvzcl adantr nvmval syl111anc
      simpr eqid cc ax-1cn negcli nvscl mp3an2 nv0lid syldan eqtrd ) CKLZAELZMZ
      FADNZFOPZABNZCQRZNZUTUQUOFELZUPURVBSUOUPTUOVCUPCEFGJUAUBUOUPUEFABCVADEGVA
      UFZIHUCUDUOUPUTELZVBUTSUOUSUGLUPVEOUHUIUSABCEGIUJUKUTCVAEFGVDJULUMUN $.
      $( [29-Feb-2008] $)
  $}

  ${
    $d x y U $.  $d x y X $.
    nvmf.1 $e |- X = ( BaseSet ` U ) $.
    nvmf.3 $e |- M = ( -v ` U ) $.
    $( Mapping for the vector subtraction operation. $)
    nvmf $p |- ( U e. NrmCVec -> M : ( X X. X ) --> X ) $=
      ( vx vy cnv wcel cxp wf cv c1 cneg cns cfv co wral wa eqid cmpt2 simpl cc
      cpv simprl ax-1cn negcli nvscl mp3an2 adantrl nvgcl syl111anc ex foprab2a
      r19.21aivv sylib nvmfval feq1d mpbird ) AHIZCCJZCBKVACFGCCFLZMNZGLZAOPZQZ
      AUDPZQZUAZKZUTVHCIZGCRFCRVJUTVKFGCCUTVBCIZVDCIZSZVKUTVNSUTVLVFCIZVKUTVNUB
      UTVLVMUEUTVMVOVLUTVCUCIVMVOMUFUGVCVDVEACDVETZUHUIUJVBVFAVGCDVGTZUKULUMUOF
      GCCVHCVIVITUNUPUTVACBVIFGVEAVGBCDVQVPEUQURUS $.
      $( [23-Dec-2013] $) $( [11-Sep-2007] $)

    $( Closure law for the vector subtraction operation of a normed complex
       vector space. $)
    nvmcl $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) -> ( A M B ) e. X ) $=
      ( cnv wcel cxp wf co nvmf foprrn syl3an1 ) CHIEEJEDKAEIBEIABDLEICDEFGMABE
      EEDNO $.
      $( [11-Sep-2007] $)

    $( Vector space analog of ~ nnncan1 . $)
    nvnnncan1 $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
         ( ( A M B ) M ( A M C ) ) = ( C M B ) ) $=
      ( cnv wcel cpv cfv cabl w3a co wceq eqid nvabl bafval vsfval ablnnncan1
      sylan ) DIJDKLZMJAFJBFJCFJNABEOACEOEOCBEOPDUCUCQZRABCEUCFDUCFGUDSDUCEUDHT
      UAUB $.
      $( [7-Mar-2008] $)

    $( Vector space analog of ~ nnncan2 . $)
    nvnnncan2 $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
         ( ( A M C ) M ( B M C ) ) = ( A M B ) ) $=
      ( cnv wcel cpv cfv cgr w3a co wceq eqid nvgrp bafval vsfval grpnnncan2
      sylan ) DIJDKLZMJAFJBFJCFJNACEOBCEOEOABEOPDUCUCQZRABCEUCFDUCFGUDSDUCEUDHT
      UAUB $.
      $( [15-Feb-2008] $)
  $}

  ${
    nvmdi.1 $e |- X = ( BaseSet ` U ) $.
    nvmdi.3 $e |- M = ( -v ` U ) $.
    nvmdi.4 $e |- S = ( .s ` U ) $.
    $( Distributive law for scalar product over subtraction. $)
    nvmdi $p |- ( ( U e. NrmCVec /\ ( A e. CC /\ B e. X /\ C e. X ) ) ->
       ( A S ( B M C ) ) = ( ( A S B ) M ( A S C ) ) ) $=
      ( cnv wcel cc w3a c1 co wceq nvscl opreq2d nvmval wa simpr1 simpr2 ax-1cn
      cneg cpv cfv negcli mp3an2 3ad2antr3 3jca eqid nvdi syldan nvscom mp3anr2
      3adantr2 eqtrd 3adant3r1 simpl 3adant3r3 3adant3r2 syl111anc 3eqtr4d ) EK
      LZAMLZBGLZCGLZNZUAZABOUEZCDPZEUFUGZPZDPZABDPZVKACDPZDPZVMPZABCFPZDPVPVQFP
      ZVJVOVPAVLDPZVMPZVSVEVIVFVGVLGLZNVOWCQVJVFVGWDVEVFVGVHUBVEVFVGVHUCVEVFVHW
      DVGVEVKMLZVHWDOUDUHZVKCDEGHJRUIUJUKABVLDEVMGHVMULZJUMUNVJWBVRVPVMVEVFVHWB
      VRQZVGVEVFWEVHWHWFAVKCDEGHJUOUPUQSURVJVTVNADVEVGVHVTVNQVFBCDEVMFGHWGJITUS
      SVJVEVPGLZVQGLZWAVSQVEVIUTVEVFVGWIVHABDEGHJRVAVEVFVHWJVGACDEGHJRVBVPVQDEV
      MFGHWGJITVCVD $.
      $( [14-Feb-2008] $)
  $}

  ${
    nvnegneg.1 $e |- X = ( BaseSet ` U ) $.
    nvnegneg.4 $e |- S = ( .s ` U ) $.
    $( Double negative of a vector. $)
    nvnegneg $p |- ( ( U e. NrmCVec /\ A e. X ) ->
                  ( -u 1 S ( -u 1 S A ) ) = A ) $=
      ( cnv wcel wa c1 cneg co cpv cfv cgn wceq cc ax-1cn eqid nvinv negcli cgr
      nvscl mp3an2 syldan fveq2d nvgrp bafval grp2inv sylan 3eqtrd ) CGHZADHZIZ
      JKZUOABLZBLZUPCMNZONZNZAUSNZUSNZAULUMUPDHZUQUTPULUOQHUMVCJRUAUOABCDEFUCUD
      UPBCURUSDEURSZFUSSZTUEUNUPVAUSABCURUSDEVDFVETUFULURUBHUMVBAPCURVDUGAURUSD
      CURDEVDUHVEUIUJUK $.
      $( [4-Dec-2007] $)
  $}

  ${
    nvmul0or.1 $e |- X = ( BaseSet ` U ) $.
    nvmul0or.4 $e |- S = ( .s ` U ) $.
    nvmul0or.6 $e |- Z = ( 0vec ` U ) $.
    $( If a scalar product is zero, one of its factors must be zero. $)
    nvmul0or $p |- ( ( U e. NrmCVec /\ A e. CC /\ B e. X ) ->
                    ( ( A S B ) = Z <-> ( A = 0 \/ B = Z ) ) ) $=
      ( wcel cc co wceq cc0 wa c1 opreq2 3ad2antl2 3adant2 3eqtr3d cnv wo df-ne
      w3a wn wne cdiv ad2antlr recid2 opreq1d simpl1 reccl simpl2 simpl3 nvsass
      cmul syl13anc adantr adantlr nvsz sylan2 anassrs 3adantl3 ex syl5bir orrd
      nvsid wi nv0 opreq1 eqeq1d syl5ibrcom 3adant3 jaod impbid ) DUAJZAKJZBEJZ
      UDZABCLZFMZANMZBFMZUBZVSWAWDVSWAOZWBWCWBUEANUFZWEWCANUCWEWFWCWEWFOPAUGLZV
      TCLZWGFCLZBFWAWHWIMVSWFVTFWGCQUHVSWFWHBMWAVSWFOZWGAUPLZBCLZPBCLZWHBVQVPWF
      WLWMMVRVQWFOZWKPBCAUIUJRWJVPWGKJZVQVRWLWHMVPVQVRWFUKVQVPWFWOVRAULZRVPVQVR
      WFUMVPVQVRWFUNWGABCDEGHUOUQVSWMBMZWFVPVRWQVQBCDEGHVGSURTUSVSWFWIFMZWAVPVQ
      WFWRVRVPVQWFWRWNVPWOWRWPWGCDFHIUTVAVBVCUSTVDVEVFVDVSWBWAWCVPVRWBWAVHVQVPV
      ROWAWBNBCLZFMBCDEFGHIVIWBVTWSFANBCVJVKVLSVPVQWCWAVHVRVPVQOWAWCAFCLZFMACDF
      HIUTWCVTWTFBFACQVKVLVMVNVO $.
      $( [6-Dec-2007] $)
  $}

  ${
    nvrinv.1 $e |- X = ( BaseSet ` U ) $.
    nvrinv.2 $e |- G = ( +v ` U ) $.
    nvrinv.4 $e |- S = ( .s ` U ) $.
    nvrinv.6 $e |- Z = ( 0vec ` U ) $.
    $( A vector minus itself. $)
    nvrinv $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( A G ( -u 1 S A ) ) = Z ) $=
      ( cnv wcel wa cgn cfv co cgi c1 wceq eqid cgr nvgrp bafval grporinv sylan
      cneg nvinv opreq2d 0vfval adantr 3eqtr4d ) CKLZAELZMZAADNOZOZDPZDQOZARUFA
      BPZDPFULDUALUMUQURSCDHUBAURDUOECDEGHUCURTUOTZUDUEUNUSUPADABCDUOEGHIUTUGUH
      ULFURSUMCDKFHJUIUJUK $.
      $( [21-Dec-2013] $) $( [4-Dec-2007] $)

    $( Minus a vector plus itself. $)
    nvlinv $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( ( -u 1 S A ) G A ) = Z ) $=
      ( cnv wcel wa cgn cfv co cgi c1 wceq eqid cgr nvgrp bafval grpolinv sylan
      cneg nvinv opreq1d 0vfval adantr 3eqtr4d ) CKLZAELZMZADNOZOZADPZDQOZRUFAB
      PZADPFULDUALUMUQURSCDHUBAURDUOECDEGHUCURTUOTZUDUEUNUSUPADABCDUOEGHIUTUGUH
      ULFURSUMCDKFHJUIUJUK $.
      $( [21-Dec-2013] $) $( [4-Dec-2007] $)
  $}

  ${
    nvsubadd.1 $e |- X = ( BaseSet ` U ) $.
    nvsubadd.2 $e |- G = ( +v ` U ) $.
    nvsubadd.3 $e |- M = ( -v ` U ) $.
    $( Relationship between vector subtraction and addition. $)
    nvsubadd $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                ( ( A M B ) = C <-> ( B G C ) = A ) ) $=
      ( wcel w3a wa co wceq c1 cfv eqid 3adant3r3 eqeq1d cnv cneg cns nvmval wb
      cc ax-1cn negcli nvscl mp3an2 3adant2 nvgcl syld3an3 simpr3 simpr2 nvlcan
      syldan simprr simprl adantrl nvadd12 nvrinv opreq2d nv0rid adantrr 3eqtrd
      3jca cn0v 3adantr3 bitr3d eqcom syl6bb bitrd ) DUAKZAGKZBGKZCGKZLZMZABFNZ
      COAPUBZBDUCQZNZENZCOZBCENZAOZVSVTWDCVNVOVPVTWDOVQABWBDEFGHIWBRZJUDSTVSWEA
      WFOZWGVSBWDENZWFOZWEWIVNVRWDGKZVQVPLWKWEUEVSWLVQVPVNVOVPWLVQVNVOVPWCGKZWL
      VNVPWMVOVNWAUFKVPWMPUGUHWABWBDGHWHUIUJZUKAWCDEGHIULUMSVNVOVPVQUNVNVOVPVQU
      OVGWDCBDEGHIUPUQVSWJAWFVNVOVPWJAOVQVNVOVPMZMZWJABWCENZENZADVHQZENZAVNWOVP
      VOWMLWJWROWPVPVOWMVNVOVPURVNVOVPUSVNVPWMVOWNUTVGBAWCDEGHIVAUQWPWQWSAEVNVP
      WQWSOVOBWBDEGWSHIWHWSRZVBUTVCVNVOWTAOVPADEGWSHIXAVDVEVFVITVJAWFVKVLVM $.
      $( [14-Dec-2007] $)

    $( Cancellation law for vector subtraction. $)
    nvpncan2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                 ( ( A G B ) M A ) = B ) $=
      ( cnv wcel w3a co c1 cneg cns cfv wceq eqid 3adant3 simp1 nvgcl syl111anc
      simp2 nvmval simp3 cc ax-1cn negcli nvscl mp3an2 nvadd23 syl13anc opreq1d
      cn0v nvrinv nv0lid 3adant2 eqtrd 3eqtrd ) CJKZAFKZBFKZLZABDMZAEMZVENOZACP
      QZMZDMZAVIDMZBDMZBVDVAVEFKVBVFVJRVAVBVCUAZABCDFGHUBVAVBVCUDZVEAVHCDEFGHVH
      SZIUEUCVDVAVBVCVIFKZVJVLRVMVNVAVBVCUFVAVBVPVCVAVGUGKVBVPNUHUIVGAVHCFGVOUJ
      UKTABVICDFGHULUMVDVLCUOQZBDMZBVDVKVQBDVAVBVKVQRVCAVHCDFVQGHVOVQSZUPTUNVAV
      CVRBRVBBCDFVQGHVSUQURUSUT $.
      $( [27-Dec-2007] $)

    $( Cancellation law for vector subtraction. $)
    nvpncan $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                 ( ( A G B ) M B ) = A ) $=
      ( cnv wcel co wceq w3a nvcom opreq1d nvpncan2 eqtr3d 3com23 ) CJKZBFKZAFK
      ZABDLZBELZAMTUAUBNZBADLZBELUDAUEUFUCBEBACDFGHOPBACDEFGHIQRS $.
      $( [24-Jan-2008] $)

    $( Associative-type law for vector addition and subtraction. $)
    nvaddsubass $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
              ( ( A G B ) M C ) = ( A G ( B M C ) ) ) $=
      ( cnv wcel cgr w3a co wceq nvgrp bafval vsfval grpmuldivass sylan ) DKLEM
      LAGLBGLCGLNABEOCFOABCFOEOPDEIQABCFEGDEGHIRDEFIJSTUA $.
      $( [24-Jan-2008] $)

    $( Commutative/associative law for vector addition and subtraction. $)
    nvaddsub $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
      ( ( A G B ) M C ) = ( ( A M C ) G B ) ) $=
      ( cnv wcel cabl w3a co wceq nvabl bafval vsfval ablmuldiv sylan ) DKLEMLA
      GLBGLCGLNABEOCFOACFOBEOPDEIQABCFEGDEGHIRDEFIJSTUA $.
      $( [24-Jan-2008] $)

    $( Cancellation law for a normed complex vector space. $)
    nvnpcan $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
         ( ( A M B ) G B ) = A ) $=
      ( cnv wcel w3a co wceq wa simprl simprr 3jca nvaddsub syldan 3impb eqtr3d
      nvpncan ) CJKZAFKZBFKZLABDMBEMZABEMBDMZAUDUEUFUGUHNZUDUEUFOZUEUFUFLUIUDUJ
      OUEUFUFUDUEUFPUDUEUFQZUKRABBCDEFGHISTUAABCDEFGHIUCUB $.
      $( [24-Jan-2008] $)

    $( Rearrangement of 4 terms in a mixed vector addition and subtraction. $)
    nvaddsub4 $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X ) /\ ( C e. X /\
       D e. X ) ) -> ( ( A G B ) M ( C G D ) ) = ( ( A M C ) G ( B M D ) ) ) $=
      ( wcel wa co c1 wceq 3adant2 nvscl mp3an2 nvmval cnv w3a cc ax-1cn negcli
      cneg cns cfv eqid nvdi mp3anr1 opreq2d anim12i anandis nvadd4 eqtrd simp1
      syld3an3 nvgcl 3expb 3adant3 3adant3r 3adant2r 3adant3l 3adant2l opreq12d
      syl111anc 3eqtr4d ) EUALZAHLZBHLZMZCHLZDHLZMZUBZABFNZOUFZCDFNZEUGUHZNZFNZ
      AVRCVTNZFNZBVRDVTNZFNZFNZVQVSGNZACGNZBDGNZFNVPWBVQWCWEFNZFNZWGVPWAWKVQFVI
      VOWAWKPZVLVIVRUCLZVMVNWMOUDUEZVRCDVTEFHIJVTUIZUJUKQULVIVLVOWCHLZWEHLZMZWL
      WGPVIVOWSVLVIVMVNWSVIVMMWQVIVNMWRVIWNVMWQWOVRCVTEHIWPRSVIWNVNWRWOVRDVTEHI
      WPRSUMUNQABWCWEEFHIJUOURUPVPVIVQHLZVSHLZWHWBPVIVLVOUQVIVLWTVOVIVJVKWTABEF
      HIJUSUTVAVIVOXAVLVIVMVNXACDEFHIJUSUTQVQVSVTEFGHIJWPKTVGVPWIWDWJWFFVIVJVOW
      IWDPZVKVIVJVMXBVNACVTEFGHIJWPKTVBVCVIVKVOWJWFPZVJVIVKVNXCVMBDVTEFGHIJWPKT
      VDVEVFVH $.
      $( [8-Feb-2008] $)
  $}

  ${
    nvsubsub23.1 $e |- X = ( BaseSet ` U ) $.
    nvsubsub23.3 $e |- M = ( -v ` U ) $.
    $( Swap subtrahend and result of vector subtraction. $)
    nvsubsub23 $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                ( ( A M B ) = C <-> ( A M C ) = B ) ) $=
      ( cnv wcel w3a wa cpv cfv co wceq eqid nvcom 3adant3r1 nvsubadd eqeq1d wb
      3ancomb sylan2b 3bitr4d ) DIJZAFJZBFJZCFJZKZLZBCDMNZOZAPCBULOZAPZABEOCPAC
      EOBPZUKUMUNAUFUHUIUMUNPUGBCDULFGULQZRSUAABCDULEFGUQHTUJUFUGUIUHKUPUOUBUGU
      HUIUCACBDULEFGUQHTUDUE $.
      $( [14-Dec-2007] $)

    $( Cancellation law for a normed complex vector space. $)
    nvnncan $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
         ( A M ( A M B ) ) = B ) $=
      ( wcel co c1 cfv wceq nvmval ax-1cn nvscl mp3an2 3adant2 syl13anc opreq2d
      eqid cnv w3a cneg cns cpv cn0v nvmcl syld3an3 simp1 negcli a1i simp2 nvdi
      cc cmul mul2negi mulid1i eqtri opreq1i nvsid syl5eq nvsass mp3anr1 mpanr1
      eqtr3d 3eqtr4d 3adant3 simp3 nvass nvrinv opreq1d 3eqtr2d nv0lid 3eqtrd
      wa ) CUAHZAEHZBEHZUBZAABDIZDIZAJUCZVTCUDKZIZCUEKZIZCUFKZBWEIZBVPVQVRVTEHW
      AWFLABCDEFGUGAVTWCCWEDEFWETZWCTZGMUHVSWFAWBAWCIZBWEIZWEIZAWKWEIZBWEIZWHVS
      WDWLAWEVSWBAWBBWCIZWEIZWCIZWKWBWPWCIZWEIZWDWLVSVPWBUNHZVQWPEHZWRWTLVPVQVR
      UIZXAVSJNUJZUKVPVQVRULZVPVRXBVQVPXAVRXBXDWBBWCCEFWJOPQWBAWPWCCWEEFWIWJUMR
      VSVTWQWBWCABWCCWEDEFWIWJGMSVSBWSWKWEVPVRBWSLVQVPVRVOZWBWBUOIZBWCIZBWSXFXH
      JBWCIBXGJBWCXGJJUOIJJJNNUPJNUQURUSBWCCEFWJUTVAVPXAVRXHWSLZXDVPXAXAVRXIXDW
      BWBBWCCEFWJVBVCVDVEQSVFSVSVPVQWKEHZVRWOWMLXCXEVPVQXJVRVPXAVQXJXDWBAWCCEFW
      JOPVGVPVQVRVHAWKBCWEEFWIVIRVSWNWGBWEVPVQWNWGLVRAWCCWEEWGFWIWJWGTZVJVGVKVL
      VPVRWHBLVQBCWEEWGFWIXKVMQVN $.
      $( [17-Dec-2007] $)
  $}

  ${
    nvmeq0.1 $e |- X = ( BaseSet ` U ) $.
    nvmeq0.3 $e |- M = ( -v ` U ) $.
    nvmeq0.5 $e |- Z = ( 0vec ` U ) $.
    $( The difference between two vectors is zero iff they are equal. $)
    nvmeq0 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
            ( ( A M B ) = Z <-> A = B ) ) $=
      ( cnv wcel w3a co cpv cfv wceq wb wa nvmcl 3expb nvzcl adantr simprr 3jca
      eqid nvrcan syldan 3impb nvnpcan nv0lid 3adant2 eqeq12d bitr3d ) CJKZAEKZ
      BEKZLZABDMZBCNOZMZFBUSMZPZURFPZABPUNUOUPVBVCQZUNUOUPRZUREKZFEKZUPLVDUNVER
      VFVGUPUNUOUPVFABCDEGHSTUNVGVECEFGIUAUBUNUOUPUCUDURFBCUSEGUSUEZUFUGUHUQUTA
      VABABCUSDEGVHHUIUNUPVABPUOBCUSEFGVHIUJUKULUM $.
      $( [24-Jan-2008] $)

    $( A vector minus itself is the zero vector. $)
    nvmid $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( A M A ) = Z ) $=
      ( cnv wcel wa co wceq eqid wb nvmeq0 3anidm23 mpbiri ) BIJZADJZKAACLEMZAA
      MZANSTUAUBOAABCDEFGHPQR $.
      $( [28-Jan-2008] $)
  $}

  ${
    $d x y N $.  $d x y U $.  $d x y X $.
    nvf.1 $e |- X = ( BaseSet ` U ) $.
    nvf.6 $e |- N = ( norm ` U ) $.
    $( Mapping for the norm function. $)
    nvf $p |- ( U e. NrmCVec -> N : X --> RR ) $=
      ( vx vy cnv wcel cpv cfv cns cop cvc cr cv wceq co wral eqid wf cn0v cabs
      cc0 wi cmul cc caddc cle wbr w3a nvi simp2d ) AHIAJKZALKZMNICOBUAFPZBKZUD
      QUPAUBKZQUEGPZUPUORBKUSUCKUQUFRQGUGSUPUSUNRBKUQUSBKUHRUIUJGCSUKFCSFGUOAUN
      BCURDUNTUOTURTEULUM $.
      $( [6-Sep-2007] $) $( [11-Nov-2006] $)

    $( The norm of a normed complex vector space is a real number. $)
    nvcl $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( N ` A ) e. RR ) $=
      ( cnv wcel cr wf cfv nvf ffvelrn sylan ) BGHDICJADHACKIHBCDEFLDIACMN $.
      $( [8-Sep-2007] $) $( [24-Nov-2006] $)

    ${
      nvcli.9 $e |- U e. NrmCVec $.
      nvcli.7 $e |- A e. X $.
      $( The norm of a normed complex vector space is a real number. $)
      nvcli $p |- ( N ` A ) e. RR $=
        ( cnv wcel cfv cr nvcl mp2an ) BIJADJACKLJGHABCDEFMN $.
        $( [20-Apr-2007] $)
    $}
  $}

  ${
    nvdm.2 $e |- G = ( +v ` U ) $.
    nvdm.6 $e |- N = ( norm ` U ) $.
    $( Two ways to express the set of vectors in a normed complex vector
       space. $)
    nvdm $p |- ( U e. NrmCVec -> ( X = dom N <-> X = ran G ) ) $=
      ( cnv wcel cdm crn cr wf wceq cba cfv eqid bafval eqcomi nvf fdm eqeq2d
      syl ) AGHZCIZBJZDUCUEKCLUDUEMACUEANOZUEABUFUFPEQRFSUEKCTUBUA $.
      $( [25-Jun-2008] $) $( [31-Jan-2007] $)
  $}

  ${
    $d y A $.  $d x y B $.  $d x y N $.  $d x y S $.  $d x y U $.  $d x y X $.
    nvs.1 $e |- X = ( BaseSet ` U ) $.
    nvs.4 $e |- S = ( .s ` U ) $.
    nvs.6 $e |- N = ( norm ` U ) $.
    $( Proportionality property of the norm of a scalar product in a normed
       complex vector space. $)
    nvs $p |- ( ( U e. NrmCVec /\ A e. CC /\ B e. X ) ->
               ( N ` ( A S B ) ) = ( ( abs ` A ) x. ( N ` B ) ) ) $=
      ( vy vx wcel cc co cfv cabs cmul wceq cv wral cnv wa cpv cop cvc cc0 cn0v
      cr wf wi caddc cle wbr w3a eqid nvi simp3 simp2 ralimi 3syl opreq2 fveq2d
      fveq2 opreq2d eqeq12d opreq1 opreq1d rcla42v syl5 3impia 3com13 ) BFLZAML
      ZDUALZABCNZEOZAPOZBEOZQNZRZVLVMVNVTVNJSZKSZCNZEOZWAPOZWBEOZQNZRZJMTZKFTZV
      LVMUBVTVNDUCOZCUDUELZFUHEUIZWFUFRWBDUGOZRUJZWIWBWAWKNEOWFWAEOUKNULUMJFTZU
      NZKFTZUNWRWJKJCDWKEFWNGWKUOHWNUOIUPWLWMWRUQWQWIKFWOWIWPURUSUTWHVTWABCNZEO
      ZWEVRQNZRKJBAFMWBBRZWDWTWGXAXBWCWSEWBBWACVAVBXBWFVRWEQWBBEVCVDVEWAARZWTVP
      XAVSXCWSVOEWAABCVFVBXCWEVQVRQWAAPVCVGVEVHVIVJVK $.
      $( [21-Sep-2007] $) $( [11-Nov-2006] $)

    $( The norm of a scalar product with a nonnegative real. $)
    nvsge0 $p |- ( ( U e. NrmCVec /\ ( A e. RR /\ 0 <_ A ) /\ B e. X ) ->
               ( N ` ( A S B ) ) = ( A x. ( N ` B ) ) ) $=
      ( cnv wcel cr cc0 cle wbr wa co cfv cmul wceq w3a cabs cc recn adantr nvs
      syl3an2 absid 3ad2ant2 opreq1d eqtrd ) DJKZALKZMANOZPZBFKZUAZABCQERZAUBRZ
      BERZSQZAUTSQUOULAUCKZUPURVATUMVBUNAUDUEABCDEFGHIUFUGUQUSAUTSUOULUSATUPAUH
      UIUJUK $.
      $( [1-Jan-2008] $)

    $( The norm of the negative of a vector. $)
    nvm1 $p |- ( ( U e. NrmCVec /\ A e. X ) ->
               ( N ` ( -u 1 S A ) ) = ( N ` A ) ) $=
      ( cnv wcel c1 co cfv cabs cmul cc wceq ax-1cn cc0 1re cneg negcli absnegi
      nvs mp3an2 cle wbr 0re lt01 ltleii absidi ax-mp eqtri opreq1i nvcl mulid2
      wa recnd syl syl5eq eqtrd ) CIJZAEJZUQZKUAZABLDMZVENMZADMZOLZVHVBVEPJVCVF
      VIQKRUBVEABCDEFGHUDUEVDVIKVHOLZVHVGKVHOVGKNMZKKRUCSKUFUGVKKQSKUHTUIUJKTUK
      ULUMUNVDVHPJVJVHQVDVHACDEFHUOURVHUPUSUTVA $.
      $( [24-Sep-2007] $) $( [28-Nov-2006] $)
  $}

  ${
    nvdif.1 $e |- X = ( BaseSet ` U ) $.
    nvdif.2 $e |- G = ( +v ` U ) $.
    nvdif.4 $e |- S = ( .s ` U ) $.
    nvdif.6 $e |- N = ( norm ` U ) $.
    $( MAY BE REVISED - make this obsolete in favor of nvsub $)
    $( The norm of the difference between two vectors. $)
    nvdif $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( N ` ( A G ( -u 1 S B ) ) ) = ( N ` ( B G ( -u 1 S A ) ) ) ) $=
      ( wcel c1 co cfv wceq nvscl mp3an2 3adant3 syl111anc cnv w3a simp1 ax-1cn
      cneg negcli a1i simp3 nvdi syl13anc nvnegneg opreq2d 3adant2 simp2 3eqtrd
      cc nvcom fveq2d nvgcl nvm1 syl11anc eqtr3d ) DUALZAGLZBGLZUBZMUEZBVGACNZE
      NZCNZFOZAVGBCNZENZFOVIFOZVFVJVMFVFVJVLVGVHCNZENZVLAENZVMVFVCVGUPLZVEVHGLZ
      VJVPPVCVDVEUCZVRVFMUDUFZUGVCVDVEUHZVCVDVSVEVCVRVDVSWAVGACDGHJQRSZVGBVHCDE
      GHIJUIUJVFVOAVLEVCVDVOAPVEACDGHJUKSULVFVCVLGLZVDVQVMPVTVCVEWDVDVCVRVEWDWA
      VGBCDGHJQRUMVCVDVEUNVLADEGHIUQTUOURVFVCVIGLZVKVNPVTVFVCVEVSWEVTWBWCBVHDEG
      HIUSTVICDFGHJKUTVAVB $.
      $( [25-Sep-2007] $) $( [1-Dec-2006] $)

    $( The norm of a vector plus the imaginary scalar product of another. $)
    nvpi $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( N ` ( A G ( _i S B ) ) ) = ( N ` ( B G ( -u _i S A ) ) ) ) $=
      ( wcel c1 ci co cfv cmul cneg wceq ax-icn cnv w3a cc simp1 mp3an2 3adant2
      cr nvscl nvgcl syld3an3 nvcl syl11anc recnd mulid2 syl cabs absnegi eqtri
      absi opreq1i negcli simp2 nvdi mp3anr1 syl12anc wa nvsass mpanr1 mulneg1i
      nvs ixi negeqi ax-1cn negnegi 3eqtri nvsid syl5eq eqtr3d opreq2d syld3an2
      3adant3 nvcom 3eqtrd fveq2d syl5eqr ) DUALZAGLZBGLZUBZMANBCOZEOZFPZQOZWLB
      NRZACOZEOZFPZWIWLUCLWMWLSWIWLWIWFWKGLZWLUGLWFWGWHUDZWFWGWHWJGLZWRWFWHWTWG
      WFNUCLZWHWTTNBCDGHJUHUEUFZAWJDEGHIUIUJZWKDFGHKUKULUMWLUNUOWIWMWNUPPZWLQOZ
      WQXDMWLQXDNUPPMNTUQUSURUTWIWNWKCOZFPZXEWQWIWFWRXGXESZWSXCWFWNUCLZWRXHNTVA
      ZWNWKCDFGHJKVJUEULWIXFWPFWIXFWOWNWJCOZEOZWOBEOZWPWIWFWGWTXFXLSZWSWFWGWHVB
      XBWFXIWGWTXNXJWNAWJCDEGHIJVCVDVEWIXKBWOEWFWHXKBSWGWFWHVFZWNNQOZBCOZXKBWFX
      AWHXQXKSZTWFXIXAWHXRXJWNNBCDGHJVGVDVHXOXQMBCOBXPMBCXPNNQOZRMRZRMNNTTVIXSX
      TVKVLMVMVNVOUTBCDGHJVPVQVRUFVSWFWOGLZWGWHXMWPSWFWGYAWHWFXIWGYAXJWNACDGHJU
      HUEWAWOBDEGHIWBVTWCWDVRWEVR $.
      $( [29-Oct-2007] $) $( [2-Feb-2007] $)
  $}

  ${
    nvsub.1 $e |- X = ( BaseSet ` U ) $.
    nvsub.3 $e |- M = ( -v ` U ) $.
    nvsub.6 $e |- N = ( norm ` U ) $.
    $( The norm of the difference between two vectors. $)
    nvsub $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
               ( N ` ( A M B ) ) = ( N ` ( B M A ) ) ) $=
      ( cnv wcel w3a c1 cneg cns cfv co eqid nvmval fveq2d nvdif 3com23 3eqtr4d
      cpv wceq ) CJKZAFKZBFKZLZAMNZBCOPZQCUDPZQZEPBUJAUKQULQZEPABDQZEPBADQZEPAB
      UKCULEFGULRZUKRZIUAUIUOUMEABUKCULDFGUQURHSTUIUPUNEUFUHUGUPUNUEBAUKCULDFGU
      QURHSUBTUC $.
      $( [14-Dec-2007] $)
  $}

  ${
    nvz0.5 $e |- Z = ( 0vec ` U ) $.
    nvz0.6 $e |- N = ( norm ` U ) $.
    $( The norm of a zero vector is zero. $)
    nvz0 $p |- ( U e. NrmCVec -> ( N ` Z ) = 0 ) $=
      ( cnv wcel cc0 cns cfv co cmul cba wceq eqid nvzcl cr wa 0re mpdan pm3.2i
      cle wbr leidi nvsge0 mp3an2 nv0 fveq2d cc nvcl recnd mul02 syl 3eqtr3d )
      AFGZHCAIJZKZBJZHCBJZLKZUSHUOCAMJZGZURUTNZAVACVAOZDPZUOHQGZHHUBUCZRVBVCVFV
      GSHSUDUAHCUPABVAVDUPOZEUEUFTUOUQCBUOVBUQCNVECUPAVACVDVHDUGTUHUOUSUIGZUTHN
      UOVBVIVEUOVBRUSCABVAVDEUJUKTUSULUMUN $.
      $( [25-Sep-2007] $) $( [24-Nov-2006] $)
  $}

  ${
    $d x A $.  $d x y N $.  $d x y U $.  $d x y X $.  $d x Z $.
    nvz.1 $e |- X = ( BaseSet ` U ) $.
    nvz.5 $e |- Z = ( 0vec ` U ) $.
    nvz.6 $e |- N = ( norm ` U ) $.
    $( The norm of a vector is zero iff the vector is zero.  First part of
       Problem 2 of [Kreyszig] p. 64. $)
    nvz $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( ( N ` A ) = 0 <-> A = Z ) ) $=
      ( vx vy wcel cfv cc0 wceq wi cv co wral eqid fveq2 cnv wa cns cabs cc cpv
      cmul caddc cle wbr w3a cop cvc cr wf nvi simp3d simp1 ralimi eqeq1d eqeq1
      imbi12d rcla4cv 3syl imp nvz0 sylan9eqr ex adantr impbid ) BUAKZADKZUBACL
      ZMNZAENZVKVLVNVOOZVKIPZCLZMNZVQENZOZJPZVQBUCLZQCLWBUDLVRUGQNJUERZVQWBBUFL
      ZQCLVRWBCLUHQUIUJJDRZUKZIDRZWAIDRVLVPOVKWEWCULUMKDUNCUOWHIJWCBWECDEFWESWC
      SGHUPUQWGWAIDWAWDWFURUSWAVPIADVQANZVSVNVTVOWIVRVMMVQACTUTVQAEVAVBVCVDVEVK
      VOVNOVLVKVOVNVOVKVMECLMAECTBCEGHVFVGVHVIVJ $.
      $( [30-Sep-2007] $) $( [24-Nov-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y G $.  $d x y N $.  $d x y U $.  $d x y X $.
    nvtri.1 $e |- X = ( BaseSet ` U ) $.
    nvtri.2 $e |- G = ( +v ` U ) $.
    nvtri.6 $e |- N = ( norm ` U ) $.
    $( Triangle inequality for the norm of a normed complex vector space. $)
    nvtri $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
          ( N ` ( A G B ) ) <_ ( ( N ` A ) + ( N ` B ) ) ) $=
      ( vx vy wcel co cfv caddc cle wbr cv wral wceq cnv wa cn0v c1st c2nd cabs
      cc0 wi cmul cc w3a cop cvc cr wf cns eqid smfval eqcomi nvi simp3d ralimi
      simp3 syl opreq1 fveq2d fveq2 opreq1d breq12d opreq2 opreq2d rcla42v syl5
      3impia 3comr ) AFLZBFLZCUALZABDMZENZAENZBENZOMZPQZVPVQVRWDVRJRZKRZDMZENZW
      EENZWFENZOMZPQZKFSZJFSZVPVQUBWDVRWIUGTWECUCNZTUHZWFWECUDNUENZMENWFUFNWIUI
      MTKUJSZWMUKZJFSZWNVRDWQULUMLFUNEUOWTJKWQCDEFWOGHCUPNZWQXACXAUQURUSWOUQIUT
      VAWSWMJFWPWRWMVCVBVDWLWDAWFDMZENZWAWJOMZPQJKABFFWEATZWHXCWKXDPXEWGXBEWEAW
      FDVEVFXEWIWAWJOWEAEVGVHVIWFBTZXCVTXDWCPXFXBVSEWFBADVJVFXFWJWBWAOWFBEVGVKV
      IVLVMVNVO $.
      $( [21-Dec-2013] $) $( [11-Nov-2006] $)
  $}

  ${
    nvmtri.1 $e |- X = ( BaseSet ` U ) $.
    nvmtri.3 $e |- M = ( -v ` U ) $.
    nvmtri.6 $e |- N = ( norm ` U ) $.
    $( Triangle inequality for the norm of a vector difference. $)
    nvmtri $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
          ( N ` ( A M B ) ) <_ ( ( N ` A ) + ( N ` B ) ) ) $=
      ( wcel c1 cfv co caddc cle wbr cc ax-1cn wceq cmul cnv w3a cns cpv negcli
      cneg eqid nvscl mp3an2 3adant2 syld3an3 nvmval fveq2d wa cabs nvs absnegi
      nvtri cc0 0re 1re lt01 ltleii absidi ax-mp eqtri opreq1i recnd mulid2 syl
      nvcl syl5eq eqtr2d opreq2d 3brtr4d ) CUAJZAFJZBFJZUBZAKUFZBCUCLZMZCUDLZMZ
      ELZAELZWBELZNMZABDMZELWFBELZNMOVPVQVRWBFJZWEWHOPVPVRWKVQVPVTQJZVRWKKRUEZV
      TBWACFGWAUGZUHUIUJAWBCWCEFGWCUGZIURUKVSWIWDEABWACWCDFGWOWNHULUMVSWJWGWFNV
      PVRWJWGSVQVPVRUNZWGVTUOLZWJTMZWJVPWLVRWGWRSWMVTBWACEFGWNIUPUIWPWRKWJTMZWJ
      WQKWJTWQKUOLZKKRUQUSKOPWTKSUSKUTVAVBVCKVAVDVEVFVGWPWJQJWSWJSWPWJBCEFGIVKV
      HWJVIVJVLVMUJVNVO $.
      $( [27-Dec-2007] $)

    $( Triangle inequality for the norm of a vector difference. $)
    nvmtri2 $p |- ( ( U e. NrmCVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
          ( N ` ( A M C ) ) <_ ( ( N ` ( A M B ) ) + ( N ` ( B M C ) ) ) ) $=
      ( cnv wcel w3a wa co cfv cpv caddc cle nvmcl cgr wceq nvgrp bafval vsfval
      eqid grpnpncan sylan eqcomd wbr simpl 3adant3r3 3adant3r1 nvtri syl111anc
      fveq2d eqbrtrd ) DKLZAGLZBGLZCGLZMZNZACEOZFPABEOZBCEOZDQPZOZFPZVEFPVFFPRO
      ZSVCVDVHFVCVHVDURVGUALVBVHVDUBDVGVGUFZUCABCEVGGDVGGHVKUDDVGEVKIUEUGUHUIUP
      VCURVEGLZVFGLZVIVJSUJURVBUKURUSUTVLVAABDEGHITULURUTVAVMUSBCDEGHITUMVEVFDV
      GFGHVKJUNUOUQ $.
      $( [24-Feb-2008] $)
  $}

  ${
    nvabs.1 $e |- X = ( BaseSet ` U ) $.
    nvabs.2 $e |- G = ( +v ` U ) $.
    nvabs.4 $e |- S = ( .s ` U ) $.
    nvabs.6 $e |- N = ( norm ` U ) $.
    $( Norm difference property of a normed complex vector space.  Problem 3 of
       [Kreyszig] p. 64. $)
    nvabs $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
       ( abs ` ( ( N ` A ) - ( N ` B ) ) ) <_ ( N ` ( A G ( -u 1 S B ) ) ) ) $=
      ( wcel cfv co cle wbr cr 3adant2 syl11anc wceq cnv w3a cmin cabs c1 wa wb
      cneg nvcl 3adant3 resubcl simp1 ax-1cn negcli nvscl mp3an2 nvgcl syld3an3
      cc absle nvdif negeqd caddc 3com23 nvcom cn0v simprr adantrr simprl nvass
      3jca syldan 3impb eqid nvlinv opreq2d nv0rid 3eqtrd eqtrd fveq2d eqbrtrrd
      nvtri recnd subneg renegcl syl lesub syl111anc mpbird eqbrtrd simp2 simp3
      breqtrrd syl13anc syld3an2 lesubadd mpbir2and ) DUALZAGLZBGLZUBZAFMZBFMZU
      CNZUDMAUEUHZBCNZENZFMZOPZXHUHZXDOPZXDXHOPZXAXDQLZXHQLZXIXKXLUFUGXAXBQLZXC
      QLZXMWRWSXOWTADFGHKUIUJZWRWTXPWSBDFGHKUIRZXBXCUKSXAWRXGGLZXNWRWSWTULZWRWS
      WTXFGLZXSWRWTYAWSWRXEUSLZWTYAUEUMUNZXEBCDGHJUOUPRZAXFDEGHIUQURZXGDFGHKUIS
      ZXDXHUTSXAXJBXEACNZENZFMZUHZXDOXAXHYIABCDEFGHIJKVAVBXAYJXDOPZXCXBYJUCNZOP
      ZXAXCXBYIVCNZYLOXAAYHENZFMZXCYNOXAYOBFXAYOYHAENZBWRWSWTYHGLZYOYQTWRWTWSYR
      WRWTWSYGGLZYRWRWSYSWTWRYBWSYSYCXEACDGHJUOUPZRBYGDEGHIUQURVDZAYHDEGHIVEURX
      AYQBYGAENZENZBDVFMZENZBWRWSWTYQUUCTZWRWSWTUFZWTYSWSUBUUFWRUUGUFWTYSWSWRWS
      WTVGWRWSYSWTYTVHWRWSWTVIVKBYGADEGHIVJVLVMXAUUBUUDBEWRWSUUBUUDTWTACDEGUUDH
      IJUUDVNZVOUJVPWRWTUUEBTWSBDEGUUDHIUUHVQRVRVSVTWRWSWTYRYPYNOPUUAAYHDEFGHIK
      WBURWAXAXBUSLYIUSLYLYNTXAXBXQWCXAYIXAWRYRYIQLZXTUUAYHDFGHKUISZWCXBYIWDSWM
      XAYJQLZXOXPYKYMUGXAUUIUUKUUJYIWEWFXQXRYJXBXCWGWHWIWJXAXLXBXHXCVCNZOPZXAXG
      BENZFMZXBUULOXAUUNAFXAUUNAXFBENZENZAUUDENZAXAWRWSYAWTUUNUUQTXTWRWSWTWKYDW
      RWSWTWLAXFBDEGHIVJWNXAUUPUUDAEWRWTUUPUUDTWSBCDEGUUDHIJUUHVORVPWRWSUURATWT
      ADEGUUDHIUUHVQUJVRVTWRXSWSWTUUOUULOPYEXGBDEFGHIKWBWOWAXAXOXPXNXLUUMUGXQXR
      YFXBXCXHWPWHWIWQ $.
      $( [7-Oct-2007] $) $( [4-Dec-2006] $)
  $}

  ${
    nvge0.1 $e |- X = ( BaseSet ` U ) $.
    nvge0.6 $e |- N = ( norm ` U ) $.
    $( The norm of a normed complex vector space is nonnegative.  Second part
       of Problem 2 of [Kreyszig] p. 64. $)
    nvge0 $p |- ( ( U e. NrmCVec /\ A e. X ) -> 0 <_ ( N ` A ) ) $=
      ( wcel wa c2 cfv cc0 wbr co cle c1 caddc wceq eqid ax-1cn cc cnv clt cmul
      cr nvcl 2re jctil cneg cns cpv cn0v nvz0 adantr negidi opreq1i nv0 negcli
      syl5req nvdir mp3anr1 mpanr1 opreq1d 3eqtrd fveq2d eqtr3d mpd3an3 eqbrtrd
      nvsid nvscl mp3an2 nvtri nvm1 opreq2d recnd 2times eqtr4d breqtrd prodge0
      syl 2pos syl11anc ) BUAGZADGZHZIUDGZACJZUDGZHKIUBLZKIWFUCMZNLZHKWFNLWDWGW
      EABCDEFUEZUFUGWDWJWHWDKWFOUHZABUIJZMZCJZPMZWINWDKAWNBUJJZMZCJZWPNWDBUKJZC
      JZKWSWBXAKQWCBCWTWTRZFULUMWDWTWRCWDWTOWLPMZAWMMZOAWMMZWNWQMZWRWDXDKAWMMWT
      XCKAWMOSUNUOAWMBDWTEWMRZXBUPURWBWLTGZWCXDXFQZOSUQZWBOTGXHWCXISOWLAWMBWQDE
      WQRZXGUSUTVAWDXEAWNWQAWMBDEXGVHVBVCVDVEWBWCWNDGZWSWPNLWBXHWCXLXJWLAWMBDEX
      GVIVJAWNBWQCDEXKFVKVFVGWDWPWFWFPMZWIWDWOWFWFPAWMBCDEXGFVLVMWDWFTGWIXMQWDW
      FWKVNWFVOVSVPVQVTUGIWFVRWA $.
      $( [16-Oct-2007] $) $( [28-Nov-2006] $)
  $}

  ${
    nvgt0.1 $e |- X = ( BaseSet ` U ) $.
    nvgt0.5 $e |- Z = ( 0vec ` U ) $.
    nvgt0.6 $e |- N = ( norm ` U ) $.
    $( A nonzero norm is positive. $)
    nvgt0 $p |- ( ( U e. NrmCVec /\ A e. X ) ->
                   ( A =/= Z <-> 0 < ( N ` A ) ) ) $=
      ( cnv wcel wa cfv cc0 wne clt wbr nvz necon3bid cr cle wb ne0gt0 syl11anc
      nvcl nvge0 bitr3d ) BIJADJKZACLZMNZAENMUHOPZUGUHMAEABCDEFGHQRUGUHSJMUHTPU
      IUJUAABCDFHUDABCDFHUEUHUBUCUF $.
      $( [20-Nov-2007] $)
  $}

  ${
    nv1.1 $e |- X = ( BaseSet ` U ) $.
    nv1.4 $e |- S = ( .s ` U ) $.
    nv1.5 $e |- Z = ( 0vec ` U ) $.
    nv1.6 $e |- N = ( norm ` U ) $.
    $( From any nonzero vector, construct a vector whose norm is one. $)
    nv1 $p |- ( ( U e. NrmCVec /\ A e. X /\ A =/= Z ) ->
               ( N ` ( ( 1 / ( N ` A ) ) S A ) ) = 1 ) $=
      ( wcel wne c1 cfv co cr cc0 wbr wceq syl11anc cnv w3a cdiv cmul cle simp1
      nvcl 3adant3 wa nvz necon3bid biimp3ar rereccl clt nvgt0 biimp3a 1re lt01
      ltleii divge0 mpanl12 simp2 nvsge0 syl121anc cc recnd mulcom recid 3eqtrd
      0re ) CUAKZAEKZAFLZUBZMADNZUCOZABODNZVPVOUDOZVOVPUDOZMVNVKVPPKZQVPUERZVLV
      QVRSVKVLVMUFVNVOPKZVOQLZVTVKVLWBVMACDEGJUGZUHZVKVLWCVMVKVLUIZVOQAFACDEFGI
      JUJUKULZVOUMTZVNWBQVOUNRZWAWEVKVLVMWIACDEFGIJUOUPMPKQMUERWBWIUIWAUQQMVJUQ
      URUSMVOUTVATVKVLVMVBVPABCDEGHJVCVDVNVPVEKVOVEKZVRVSSVNVPWHVFVKVLWJVMWFVOW
      DVFUHZVPVOVGTVNWJWCVSMSWKWGVOVHTVI $.
      $( [6-Dec-2007] $)
  $}

  ${
    nvop.2 $e |- G = ( +v ` U ) $.
    nvop.4 $e |- S = ( .s ` U ) $.
    nvop.6 $e |- N = ( norm ` U ) $.
    $( A complex inner product space in terms of ordered pair components. $)
    nvop $p |- ( U e. NrmCVec -> U = <. <. G , S >. , N >. ) $=
      ( cnv wcel c1st cfv c2nd cop wrel wceq nvrel 1st2nd mpan nmfval opeq2i
      eqid nvvop opeq1d syl5eqr eqtrd ) BHIZBBJKZBLKZMZCAMZDMZHNUFBUIOPBHQRUFUI
      UGDMUKDUHUGBDGSTUFUGUJDABCUGUGUAEFUBUCUDUE $.
      $( [11-Sep-2007] $)
  $}

  $( MAY BE REVISED?  is this needed? $)
  $( The vector addition and scalar product operations are not identical. $)
  nvoprne $p |- ( <. <. G , S >. , N >. e. NrmCVec -> G =/= S ) $=
    ( cop cnv wcel c1st cfv cvc wne eqid nvvc opex op1st eleq1i vcoprne sylbi
    syl ) BADZCDZEFTGHZIFZBAJZTUAUAKLUBSIFUCUASISCBAMNOABPQR $.
    $( [31-May-2008] $)

