$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Subspaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SubSp $.

  $( Extend class notation with the class of all subspaces of complex normed
     vector spaces. $)
  css $a class SubSp $.

  ${
    $d u w $.
    $( Define the class of all subspaces of complex normed vector spaces. $)
    df-ssp $a |- SubSp = ( u e. NrmCVec |->
     { w e. NrmCVec | ( ( +v ` w ) C_ ( +v ` u ) /\ ( .s ` w ) C_ ( .s ` u )
         /\ ( norm ` w ) C_ ( norm ` u ) ) } ) $.
  $}

  ${
    $d u w G $.  $d u w N $.  $d u w S $.  $d u w U $.
    sspval.g $e |- G = ( +v ` U ) $.
    sspval.s $e |- S = ( .s ` U ) $.
    sspval.n $e |- N = ( norm ` U ) $.
    sspval.h $e |- H = ( SubSp ` U ) $.
    $( The set of all subspaces of a normed complex vector space. $)
    sspval $p |- ( U e. NrmCVec -> H = { w e. NrmCVec | (
         ( +v ` w ) C_ G /\ ( .s ` w ) C_ S /\ ( norm ` w ) C_ N ) } ) $=
      ( vu cnv wcel cfv cpv wss cns cnm fveq2 fvex css cv w3a crab wceq syl6eqr
      sseq2d 3anbi123d rabbidv df-ssp cpw cxp cvv eqeltri pwex xpex wi wral cop
      elpw opelxpi syl2anbr biimpri syl2an 3impa eqid nvop eleq1d syl5ibr rabss
      wa rgen mpbir ssexi fvmpt syl5eq ) CLMECUANAUBZONZDPZVQQNZBPZVQRNZFPZUCZA
      LUDZJKCVRKUBZONZPZVTWFQNZPZWBWFRNZPZUCZALUDWELUAWFCUEZWMWDALWNWHVSWJWAWLW
      CWNWGDVRWNWGCONZDWFCOSGUFUGWNWIBVTWNWICQNZBWFCQSHUFUGWNWKFWBWNWKCRNZFWFCR
      SIUFUGUHUIAKUJWEDUKZBUKZULZFUKZULZWTXAWRWSDDWOUMGCOTUNUOBBWPUMHCQTUNUOUPF
      FWQUMICRTUNUOUPWEXBPWDVQXBMZUQZALURXDALWDXCVQLMZVRVTUSZWBUSZXBMZVSWAWCXHV
      SWAVKXFWTMZWBXAMZXHWCVSVRWRMVTWSMXIWAVRDVQOTUTVTBVQQTUTVRVTWRWSVAVBXJWCWB
      FVQRTUTVCXFWBWTXAVAVDVEXEVQXGXBVTVQVRWBVRVFVTVFWBVFVGVHVIVLWDALXBVJVMVNVO
      VP $.
      $( [16-Nov-2013] $) $( [26-Jan-2008] $)
  $}

  ${
    $d w F $.  $d w G $.  $d w M $.  $d w N $.  $d w R $.  $d w S $.  $d w U $.
    $d w W $.
    isssp.g $e |- G = ( +v ` U ) $.
    isssp.f $e |- F = ( +v ` W ) $.
    isssp.s $e |- S = ( .s ` U ) $.
    isssp.r $e |- R = ( .s ` W ) $.
    isssp.n $e |- N = ( norm ` U ) $.
    isssp.m $e |- M = ( norm ` W ) $.
    isssp.h $e |- H = ( SubSp ` U ) $.
    $( The predicate "is a subspace." $)
    isssp $p |- ( U e. NrmCVec -> ( W e. H <-> ( W e. NrmCVec /\ (
               F C_ G /\ R C_ S /\ M C_ N ) ) ) ) $=
      ( vw cnv cfv wss wcel cv cpv cns cnm w3a crab wa sspval eleq2d wceq fveq2
      syl6eqr sseq1d 3anbi123d elrab syl6bb ) CRUAZIFUAIQUBZUCSZETZUSUDSZBTZUSU
      ESZHTZUFZQRUGZUAIRUADETZABTZGHTZUFZUHURFVGIQBCEFHJLNPUIUJVFVKQIRUSIUKZVAV
      HVCVIVEVJVLUTDEVLUTIUCSDUSIUCULKUMUNVLVBABVLVBIUDSAUSIUDULMUMUNVLVDGHVLVD
      IUESGUSIUEULOUMUNUOUPUQ $.
      $( [26-Jan-2008] $)
  $}

  ${
    sspid.h $e |- H = ( SubSp ` U ) $.
    $( A normed complex vector space is a subspace of itself. $)
    sspid $p |- ( U e. NrmCVec -> U e. H ) $=
      ( cnv wcel cpv cfv wss cns cnm w3a wa ssid 3pm3.2i jctr eqid isssp mpbird
      ) ADEZABESAFGZTHZAIGZUBHZAJGZUDHZKZLSUFUAUCUETMUBMUDMNOUBUBATTBUDUDATPZUG
      UBPZUHUDPZUICQR $.
      $( [8-Apr-2008] $)
  $}

  ${
    sspnv.h $e |- H = ( SubSp ` U ) $.
    $( A subspace is a normed complex vector space. $)
    sspnv $p |- ( ( U e. NrmCVec /\ W e. H ) -> W e. NrmCVec ) $=
      ( cnv wcel cpv cfv wss cns cnm w3a eqid isssp simprbda ) AEFCBFCEFCGHZAGH
      ZICJHZAJHZICKHZAKHZILRSAPQBTUACQMPMSMRMUAMTMDNO $.
      $( [27-Jan-2008] $)
  $}

  ${
    sspba.x $e |- X = ( BaseSet ` U ) $.
    sspba.y $e |- Y = ( BaseSet ` W ) $.
    sspba.h $e |- H = ( SubSp ` U ) $.
    $( The base set of a subspace is included in the parent base set. $)
    sspba $p |- ( ( U e. NrmCVec /\ W e. H ) -> Y C_ X ) $=
      ( cnv wcel wa cpv cfv crn wss cns cnm w3a eqid bafval isssp simplbda rnss
      simp1 3syl 3sstr4g ) AIJZCBJZKZCLMZNZALMZNZEDUIUJULOZCPMZAPMZOZCQMZAQMZOZ
      RZUNUKUMOUGUHCIJVAUOUPAUJULBURUSCULSZUJSZUPSUOSUSSURSHUAUBUNUQUTUDUJULUCU
      ECUJEGVCTAULDFVBTUF $.
      $( [27-Jan-2008] $)
  $}

  ${
    $d x y F $.  $d x y G $.  $d x y H $.  $d x y U $.  $d x y W $.
    $d x y Y $.
    sspg.y $e |- Y = ( BaseSet ` W ) $.
    sspg.g $e |- G = ( +v ` U ) $.
    sspg.f $e |- F = ( +v ` W ) $.
    sspg.h $e |- H = ( SubSp ` U ) $.
    $( Vector addition on a subspace is a restriction of vector addition on the
       parent space. $)
    sspg $p |- ( ( U e. NrmCVec /\ W e. H ) -> F = ( G |` ( Y X. Y ) ) ) $=
      ( vx vy wcel wa wceq wfn wss cfv eqid 3syl cnv cxp cres wral wfun w3a cba
      cv co wf nvgf ffun funres adantr sspnv ffn syl fnresdm cns isssp simplbda
      cnm simp1 ssres eqsstr3d oprssov sylan eqcomd ex ralrimivv jctil wb sspba
      3jca xpss12 syl2anc fnssres eqfnov mpbird ) AUAMZEDMZNZBCFFUBZUCZOZWCWCOZ
      KUHZLUHZBUIZWGWHWDUIZOZLFUDKFUDZNZWBWLWFWBWKKLFFWBWGFMWHFMNZWKWBWNNWJWIWB
      WDUEZBWCPZBWDQZUFWNWJWIOWBWOWPWQVTWOWAVTAUGRZWRUBZWRCUJZCUEWOACWRWRSZHUKZ
      WSWRCULWCCUMTUNWBEUAMZWCFBUJZWPADEJUOZEBFGIUKZWCFBUPZTZWBBBWCUCZWDWBXDWPX
      IBOWBXCXDXEXFUQXGWCBURTWBBCQZEUSRZAUSRZQZEVBRZAVBRZQZUFZXJXIWDQVTWAXCXQXK
      XLABCDXNXOEHIXLSXKSXOSXNSJUTVAXJXMXPVCBCWCVDTVEVNWGWHFFWDBVFVGVHVIVJWCSVK
      WBWPWDWCPZWEWMVLXHWBCWSPZWCWSQZXRVTXSWAVTWTXSXBWSWRCUPUQUNWBFWRQZYAXTADEW
      RFXAGJVMZYBFWRFWRVOVPWSWCCVQVPKLFFFFBWDVRVPVS $.
      $( [28-Jan-2008] $)

    $( Vector addition on a subspace in terms of vector addition on the parent
       space. $)
    sspgval $p |- ( ( ( U e. NrmCVec /\ W e. H ) /\ ( A e. Y /\ B e. Y ) ) ->
                    ( A F B ) = ( A G B ) ) $=
      ( cnv wcel wa co cxp cres sspg oveqd ovres sylan9eq ) CMNGFNOZAHNBHNOABDP
      ABEHHQRZPABEPUCDUDABCDEFGHIJKLSTABHHEUAUB $.
      $( [28-Jan-2008] $)
  $}

  ${
    $d x y R $.  $d x y S $.  $d x y H $.  $d x y U $.  $d x y W $.
    $d x y Y $.
    ssps.y $e |- Y = ( BaseSet ` W ) $.
    ssps.s $e |- S = ( .s ` U ) $.
    ssps.r $e |- R = ( .s ` W ) $.
    ssps.h $e |- H = ( SubSp ` U ) $.
    $( Scalar multiplication on a subspace is a restriction of scalar
       multiplication on the parent space. $)
    ssps $p |- ( ( U e. NrmCVec /\ W e. H ) -> R = ( S |` ( CC X. Y ) ) ) $=
      ( vx vy wcel wa cc wceq wss cfv eqid 3syl cnv cxp cres cv co wral wfn w3a
      wfun cba nvsf ffun funres adantr sspnv ffn syl fnresdm cpv isssp simplbda
      wf simp2 ssres eqsstr3d 3jca oprssov sylan eqcomd ex ralrimivv jctil ssid
      cnm wb sspba xpss12 sylancr fnssres syl2anc eqfnov mpbird ) CUAMZEDMZNZAB
      OFUBZUCZPZWFWFPZKUDZLUDZAUEZWJWKWGUEZPZLFUFKOUFZNZWEWOWIWEWNKLOFWEWJOMWKF
      MNZWNWEWQNWMWLWEWGUIZAWFUGZAWGQZUHWQWMWLPWEWRWSWTWCWRWDWCOCUJRZUBZXABVBZB
      UIWRBCXAXASZHUKZXBXABULWFBUMTUNWEEUAMZWFFAVBZWSCDEJUOZAEFGIUKZWFFAUPZTZWE
      AAWFUCZWGWEXGWSXLAPWEXFXGXHXIUQXJWFAURTWEEUSRZCUSRZQZABQZEVNRZCVNRZQZUHZX
      PXLWGQWCWDXFXTABCXMXNDXQXREXNSXMSHIXRSXQSJUTVAXOXPXSVCABWFVDTVEVFWJWKOFWG
      AVGVHVIVJVKWFSVLWEWSWGWFUGZWHWPVOXKWEBXBUGZWFXBQZYAWCYBWDWCXCYBXEXBXABUPU
      QUNWEOOQFXAQYCOVMCDEXAFXDGJVPOOFXAVQVRXBWFBVSVTKLOFOFAWGWAVTWB $.
      $( [28-Jan-2008] $)

    $( Scalar multiplication on a subspace in terms of scalar multiplication on
       the parent space. $)
    sspsval $p |- ( ( ( U e. NrmCVec /\ W e. H ) /\ ( A e. CC /\ B e. Y ) ) ->
                    ( A R B ) = ( A S B ) ) $=
      ( cnv wcel wa cc co cxp cres ssps oveqd ovres sylan9eq ) EMNGFNOZAPNBHNOA
      BCQABDPHRSZQABDQUDCUEABCDEFGHIJKLTUAABPHDUBUC $.
      $( [28-Jan-2008] $)
  $}

  $( MAY BE REVISED - sspvallem doesn't save space yet, but might if another
     operation is added. $)
  $(
  @{
    @d x y F @.  @d x y G @.  @d x y H @.  @d x y U @.  @d x y W @.
    @d x y Y @.
    sspvallem.y @e |- Y = ( BaseSet ` W ) @.
    sspvallem.h @e |- H = ( SubSp ` U ) @.
    @{
      sspvallem.1 @e |- ( ( ( U e. NrmCVec /\ W e. H ) /\ ( A e. Y /\ B e. Y )
         ) -> R = S ) @.
      sspvallem.2 @e |- ( ( W e. NrmCVec /\ A e. Y /\ B e. Y ) -> P = R ) @.
      sspvallem.3 @e |- ( ( U e. NrmCVec /\ A e. ( BaseSet ` U ) /\
          B e. ( BaseSet ` U ) ) -> Q = S ) @.
      @( Lemma for ~ sspmval and others. @)
      sspvallem @p |- ( ( ( U e. NrmCVec /\ W e. H ) /\ ( A e. Y /\ B e. Y ) )
          -> P = Q ) @=
       ( cnv wcel wa wceq 3expb sspnv sylanOLD cba cfv eqid sspba sseld anim12d
        imp adantlr syldan 3eqtr4d ) GPQZIHQZRZAJQZBJQZRZREFCDMIPQZURCESZUOUSUP
        UQUTNTGHILUAUBUOURAGUCUDZQZBVAQZRZDFSZUOURVDUOUPVBUQVCUOJVAAGHIVAJVAUEK
        LUFZUGUOJVABVFUGUHUIUMVDVEUNUMVBVCVEOTUJUKUL @.
        @( [ ?] @) @( [1-Feb-2008] @)
    @}

    @{
      sspmlem.1  @e |- ( ( ( U e. NrmCVec /\ W e. H ) /\ (
       x e. Y /\ y e. Y ) ) -> ( x F y ) = ( x G y ) ) @.
      sspmlem.2 @e |- ( W e. NrmCVec -> F : ( Y X. Y ) --> R ) @.
      sspmlem.3 @e |- ( U e. NrmCVec -> G : ( ( BaseSet ` U ) X.
               ( BaseSet ` U ) ) --> S ) @.
      @( Lemma for ~ sspm and others. @)
      sspmlem @p |- ( ( U e. NrmCVec /\ W e. H ) ->
            F = ( G |` ( Y X. Y ) ) ) @=
        ( cnv wcel wa cxp cres wceq cv co wral ovres adantl eqtr4d ex
        ralrimivv eqid jctil wfn wb eqfnov wf sspnv ffn 3syl cba cfv wss
   fnssres syl adantr sspba xpss12 anidms sylancOLD mpbird ) EPQZIHQZRZFGJJSZTZ
        UAZVMVMUAZAUBZBUBZFUCZVQVRVNUCZUAZBJUDAJUDZRZVLWBVPVLWAABJJVLVQJQVRJQRZ
        WAVLWDRVSVQVRGUCZVTMWDVTWEUAVLVQVRJJGUEUFUGUHUIVMUJUKFVMULZVNVMULZVOWCU
        MVLABJJJJFVNUNVLIPQVMCFUOWFEHILUPNVMCFUQURGEUSUTZWHSZULZVMWIVAZWGVLWIVM
        GVBVJWJVKVJWIDGUOWJOWIDGUQVCVDVLJWHVAZWKEHIWHJWHUJKLVEWLWKJWHJWHVFVGVCV
        HVHVI @.
        @( [ ?] @) @( [1-Feb-2008] @)
    @}
  @}
  $)

  ${
    $d x y F $.  $d x y G $.  $d x y H $.  $d x y U $.  $d x y W $.
    $d x y Y $.
    sspmlem.y $e |- Y = ( BaseSet ` W ) $.
    sspmlem.h $e |- H = ( SubSp ` U ) $.
    sspmlem.1 $e |- ( ( ( U e. NrmCVec /\ W e. H ) /\ (
             x e. Y /\ y e. Y ) ) -> ( x F y ) = ( x G y ) ) $.
    sspmlem.2 $e |- ( W e. NrmCVec -> F : ( Y X. Y ) --> R ) $.
    sspmlem.3 $e |- ( U e. NrmCVec -> G : ( ( BaseSet ` U ) X.
             ( BaseSet ` U ) ) --> S ) $.
    $( Lemma for ~ sspm and others. $)
    sspmlem $p |- ( ( U e. NrmCVec /\ W e. H ) -> F = ( G |` ( Y X. Y ) ) ) $=
      ( wcel wa wceq co wfn cnv cres cv wral ovres adantl eqtr4d ralrimivv eqid
      cxp ex jctil wb wf sspnv ffn 3syl cba cfv wss adantr sspba xpss12 syl2anc
      syl fnssres eqfnov mpbird ) EUAPZIHPZQZFGJJUJZUBZRZVLVLRZAUCZBUCZFSZVPVQV
      MSZRZBJUDAJUDZQZVKWAVOVKVTABJJVKVPJPVQJPQZVTVKWCQVRVPVQGSZVSMWCVSWDRVKVPV
      QJJGUEUFUGUKUHVLUIULVKFVLTZVMVLTZVNWBUMVKIUAPVLCFUNWEEHILUONVLCFUPUQVKGEU
      RUSZWGUJZTZVLWHUTZWFVIWIVJVIWHDGUNWIOWHDGUPVEVAVKJWGUTZWKWJEHIWGJWGUIKLVB
      ZWLJWGJWGVCVDWHVLGVFVDABJJJJFVMVGVDVH $.
      $( [1-Feb-2008] $)
  $}

  ${
    $d x y L $.  $d x y M $.  $d x y H $.  $d x y U $.  $d x y W $.
    $d x y Y $.
    sspm.y $e |- Y = ( BaseSet ` W ) $.
    sspm.m $e |- M = ( -v ` U ) $.
    sspm.l $e |- L = ( -v ` W ) $.
    sspm.h $e |- H = ( SubSp ` U ) $.
    $( Vector addition on a subspace in terms of vector addition on the parent
       space. $)
    sspmval $p |- ( ( ( U e. NrmCVec /\ W e. H ) /\ ( A e. Y /\ B e. Y ) ) ->
                    ( A L B ) = ( A M B ) ) $=
      ( cnv wcel wa c1 cfv co wceq eqid cns cpv wi sspnv cc ax-1cn negcli nvscl
      cneg mp3an2 ex syl anim2d imp sspgval syldan sspsval mpanr1 adantrl eqtrd
      oveq2d nvmval 3expb sylan cba sspba sseld anim12d adantlr 3eqtr4d ) CMNZG
      DNZOZAHNZBHNZOZOZAPUIZBGUAQZRZGUBQZRZAVRBCUAQZRZCUBQZRZABERZABFRZVQWBAVTW
      ERZWFVMVPVNVTHNZOZWBWISVMVPWKVMVOWJVNVMGMNZVOWJUCCDGLUDZWLVOWJWLVRUENZVOW
      JPUFUGZVRBVSGHIVSTZUHUJUKULUMUNAVTCWAWEDGHIWETZWATZLUOUPVQVTWDAWEVMVOVTWD
      SZVNVMWNVOWSWOVRBVSWCCDGHIWCTZWPLUQURUSVAUTVMWLVPWGWBSZWMWLVNVOXAABVSGWAE
      HIWRWPKVBVCVDVMVPACVEQZNZBXBNZOZWHWFSZVMVPXEVMVNXCVOXDVMHXBACDGXBHXBTZILV
      FZVGVMHXBBXHVGVHUNVKXEXFVLVKXCXDXFABWCCWEFXBXGWQWTJVBVCVIUPVJ $.
      $( [28-Jan-2008] $)

    $( Vector subtraction on a subspace is a restriction of vector subtraction
       on the parent space. $)
    sspm $p |- ( ( U e. NrmCVec /\ W e. H ) -> L = ( M |` ( Y X. Y ) ) ) $=
      ( vx vy cba cfv cv sspmval nvmf eqid sspmlem ) KLFAMNZACDBEFGJKOLOABCDEFG
      HIJPECFGIQADTTRHQS $.
      $( [28-Jan-2008] $)
  $}

  ${
    $d x y F $.  $d x y G $.  $d x y H $.  $d x y U $.  $d x y W $.
    $d x y Y $.
    sspz.z $e |- Z = ( 0vec ` U ) $.
    sspz.q $e |- Q = ( 0vec ` W ) $.
    sspz.h $e |- H = ( SubSp ` U ) $.
    $( The zero vector of a subspace is the same as the parent's. $)
    sspz $p |- ( ( U e. NrmCVec /\ W e. H ) -> Q = Z ) $=
      ( cnv wcel wa cnsb cfv co cba wceq sspnv eqid syl nvmid nvzcl jca sspmval
      mpdan syl2anc sspba sseldd syldan 3eqtr3d ) BIJZDCJZKZAADLMZNZAABLMZNZAEU
      LADOMZJZURKZUNUPPULDIJZUSBCDHQZUTURURDUQAUQRZGUAZVCUBSAABCUMUODUQVBUORZUM
      RZHUCUDULUTURUNAPVAULUTURVAVCSZADUMUQAVBVEGTUEUJUKABOMZJUPEPULUQVGABCDVGU
      QVGRZVBHUFVFUGABUOVGEVHVDFTUHUI $.
      $( [28-Jan-2008] $)
  $}

  ${
    $d x H $.  $d x M $.  $d x N $.  $d x U $.  $d x W $.  $d x Y $.
    sspn.y $e |- Y = ( BaseSet ` W ) $.
    sspn.n $e |- N = ( norm ` U ) $.
    sspn.m $e |- M = ( norm ` W ) $.
    sspn.h $e |- H = ( SubSp ` U ) $.
    $( The norm on a subspace is a restriction of the norm on the parent
       space. $)
    sspn $p |- ( ( U e. NrmCVec /\ W e. H ) -> M = ( N |` Y ) ) $=
      ( vx wcel wceq cfv wss cr eqid 3syl wfn syl cnv wa cres wral wfun cdm cba
      cv wf nvf ffun funres ad2antrr sspnv ffn fnresdm cpv isssp simplbda simp3
      cns w3a ssres eqsstr3d adantr eleq2d biimpar sylan funssfv syl3anc eqcomd
      fdm ralrimiva wb sspba fnssres syl2anc eqfnfv2 mpbird ) AUALZEBLZUBZCDFUC
      ZMZKUHZCNZWEWCNZMZKFUDZWBWHKFWBWEFLZUBZWGWFWKWCUEZCWCOZWECUFZLZWGWFMVTWLW
      AWJVTAUGNZPDUIZDUEWLADWPWPQZHUJZWPPDUKFDULRUMWBWMWJWBCCFUCZWCWBFPCUIZCFSZ
      WTCMWBEUALZXAABEJUNZECFGIUJZTFPCUOZFCUPRWBEUQNZAUQNZOZEVANZAVANZOZCDOZVBZ
      XMWTWCOVTWAXCXNXJXKAXGXHBCDEXHQXGQXKQXJQHIJURUSXIXLXMUTCDFVCRVDVEWBXCWJWO
      XDXCWOWJXCWNFWEXCXAWNFMXEFPCVLTVFVGVHWEWCCVIVJVKVMWBXBWCFSZWDWIVNWBXCXAXB
      XDXEXFRWBDWPSZFWPOXOVTXPWAVTWQXPWSWPPDUOTVEABEWPFWRGJVOWPFDVPVQKFCWCVRVQV
      S $.
      $( [28-Jan-2008] $)

    $( The norm on a subspace in terms of the norm on the parent space. $)
    sspnval $p |- ( ( U e. NrmCVec /\ W e. H /\ A e. Y ) ->
                    ( M ` A ) = ( N ` A ) ) $=
      ( cnv wcel cfv wceq wa cres sspn fveq1d fvres sylan9eq 3impa ) BLMZFCMZAG
      MZADNZAENZOUCUDPZUEUFAEGQZNUGUHADUIBCDEFGHIJKRSAGETUAUB $.
      $( [28-Jan-2008] $)
  $}

  ${
    $d k A $.  $d k B $.  $d k x y H $.  $d x y P $.  $d x y Q $.
    $d k x y U $.  $d k x y W $.  $d k x y Y $.
    sspi.y $e |- Y = ( BaseSet ` W ) $.
    sspi.p $e |- P = ( .i ` U ) $.
    sspi.q $e |- Q = ( .i ` W ) $.
    sspi.h $e |- H = ( SubSp ` U ) $.
    $( The inner product on a subspace in terms of the inner product on the
       parent space. $)
    sspival $p |- ( ( ( U e. NrmCVec /\ W e. H ) /\ ( A e. Y /\ B e. Y ) ) ->
                    ( A Q B ) = ( A P B ) ) $=
      ( vk wcel wa c4 co cfv wceq eqid cnv c1 cfz ci cv cexp cns cpv cnm c2 csu
      cmul cdiv cc cn elfznn nnnn0 ax-icn expcl mpan 3syl anim1i anim2i anassrs
      cn0 an32s sspnv nvscl 3expib anim2d imp nvgcl 3expb syldan sylan 3expa wi
      sspnval sspgval sspsval adantrl oveq2d eqtrd fveq2d sylan2 sumeq2dv ipval
      syl oveq1d cba sspba sseld anim12d adantlr 3eqtr4d ) EUANZGFNZOZAHNZBHNZO
      ZOZUBPUCQZUDMUEZUFQZAXEBGUGRZQZGUHRZQZGUIRZRZUJUFQZULQZMUKZPUMQZXCXEAXEBE
      UGRZQZEUHRZQZEUIRZRZUJUFQZULQZMUKZPUMQZABDQZABCQZXBXNYDPUMXBXCXMYCMXBXDXC
      NZOZXLYBXEULYIXKYAUJUFWRXAYHXKYASZXAYHOWRWSXEUNNZWTOZOZYJWSYHWTYMWSYHWTYM
      YHWTOYLWSYHYKWTYHXDUONXDVENZYKXDPUPXDUQUDUNNYNYKURUDXDUSUTVAVBVCVDVFWRYMO
      ZXKXIXTRZYAWRYMXIHNZXKYPSZWRGUANZYMYQEFGLVGZYSYMWSXGHNZOZYQYSYMUUBYSYLUUA
      WSYSYKWTUUAXEBXFGHIXFTZVHVIZVJVKYSWSUUAYQAXGGXHHIXHTZVLVMVNVOWPWQYQYRXIEF
      XJXTGHIXTTZXJTZLVRVPVNYOXIXSXTYOXIAXGXRQZXSWRYMUUBXIUUHSWRYMUUBWRYLUUAWSW
      RYSYLUUAVQYTUUDWHVJVKAXGEXHXRFGHIXRTZUUELVSVNYOXGXQAXRWRYLXGXQSWSXEBXFXPE
      FGHIXPTZUUCLVTWAWBWCWDWCWEVDWIWBWFWIWRYSXAYFXOSZYTYSWSWTUUKABDXFGMXHXJHIU
      UEUUCUUGKWGVMVOWRXAAEWJRZNZBUULNZOZYGYESZWRXAUUOWRWSUUMWTUUNWRHUULAEFGUUL
      HUULTZILWKZWLWRHUULBUURWLWMVKWPUUOUUPWQWPUUMUUNUUPABCXPEMXRXTUULUUQUUIUUJ
      UUFJWGVMWNVNWO $.
      $( [28-Jan-2008] $)

    $( The inner product on a subspace is a restriction of the inner product on
       the parent space. $)
    sspi $p |- ( ( U e. NrmCVec /\ W e. H ) -> Q = ( P |` ( Y X. Y ) ) ) $=
      ( vx vy cc cv sspival ipf cba cfv eqid sspmlem ) KLMMCBADEFGJKNLNABCDEFGH
      IJOBEFGIPACCQRZUASHPT $.
      $( [28-Jan-2008] $)
  $}

  ${
    sspims.y $e |- Y = ( BaseSet ` W ) $.
    sspims.d $e |- D = ( IndMet ` U ) $.
    sspims.c $e |- C = ( IndMet ` W ) $.
    sspims.h $e |- H = ( SubSp ` U ) $.
    $d x y C $.  $d x y D $.  $d x y H $.  $d x y U $.  $d x y W $.
    $d x y Y $.
    $( The induced metric on a subspace in terms of the induced metric on the
       parent space. $)
    sspimsval $p |- ( ( ( U e. NrmCVec /\ W e. H ) /\ ( A e. Y /\ B e. Y ) ) ->
                    ( A C B ) = ( A D B ) ) $=
      ( cnv wcel wa cfv co wceq eqid 3expb cnsb sspnv nvmcl sylan sspnval 3expa
      cnm syldan sspmval fveq2d eqtrd imsdval cba sspba anim12d adantlr 3eqtr4d
      sseld imp ) EMNZGFNZOZAHNZBHNZOZOZABGUAPZQZGUGPZPZABEUAPZQZEUGPZPZABCQZAB
      DQZVFVJVHVMPZVNVBVEVHHNZVJVQRZVBGMNZVEVREFGLUBZVTVCVDVRABGVGHIVGSZUCTUDUT
      VAVRVSVHEFVIVMGHIVMSZVISZLUEUFUHVFVHVLVMABEFVGVKGHIVKSZWBLUIUJUKVBVTVEVOV
      JRZWAVTVCVDWFABCGVGVIHIWBWDKULTUDVBVEAEUMPZNZBWGNZOZVPVNRZVBVEWJVBVCWHVDW
      IVBHWGAEFGWGHWGSZILUNZURVBHWGBWMURUOUSUTWJWKVAUTWHWIWKABDEVKVMWGWLWEWCJUL
      TUPUHUQ $.
      $( [1-Feb-2008] $)

    $( The induced metric on a subspace is a restriction of the induced metric
       on the parent space. $)
    sspims $p |- ( ( U e. NrmCVec /\ W e. H ) -> C = ( D |` ( Y X. Y ) ) ) $=
      ( vx vy cr cv sspimsval imsdf cba cfv eqid sspmlem ) KLMMCABDEFGJKNLNABCD
      EFGHIJOAEFGIPBCCQRZUASHPT $.
      $( [1-Feb-2008] $)
  $}

  $( Define the class of all complete subspaces of a complex normed vector
     space. $)
  $(
  $c CSubSp $.
  ccs $a class SubSp $.
  df-cs $a |- CSubSp = { <. u , s >. | ( u e. NrmCVec /\ s =
       ( ( SubSp ` u ) |^| CBan ) ) } $.
  $)


