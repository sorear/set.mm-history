$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Axioms for the equality predicate
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$(
   Axioms ~ax-8 through ~ax-16 are the axioms having to do with
   equality and substitution.  To understand them easier, think of an
   antecedent of the form ` -. A. x x = y ` as 'if ` x ` and ` y ` are distinct
   variables...'  Note that all axioms except ~ax-16 and ~ax-17 are still valid
   when ` x `, ` y `, and ` z ` undergo any combination of variable
   substitutions because they do not have any $d restrictions.  The $d
   statement is required for ~ax-16 and ~ax-17 only.
$)

  $( Declare the equality predicate symbol. $)
  $c = $.  $( Equal sign (read:  'is equal to') $)

  $( Extend wff definition to include atomic formulas using the equality
     predicate. $)
  weq $a wff x = y $.

  $( Axiom of Equality.  One of the 5 equality axioms of predicate calculus.
     Axiom scheme C8' of Megill, p. 448. $)
  ax-8  $a |- ( x = y -> ( x = z -> y = z ) ) $.

  $( Axiom of Existence.  One of the 5 equality axioms of equality in predicate
     calculus.  Axiom scheme C10' of Megill, p. 448.  (See theorem ~a9e below
     to help understand its purpose.) $)
  ax-9 $a |- ( A. x ( x = y -> A. x ph ) -> ph ) $.

  $( Axiom of Quantifier Substitution.  One of the 5 equality axioms of
     predicate calculus.  Axiom scheme C11' of Megill, p. 448. $)
  ax-10 $a |- ( A. x x = y -> ( A. x ph -> A. y ph ) ) $.

  $( Axiom of Variable Substitution.  One of the 5 equality axioms of predicate
     calculus.  Axiom scheme C15' of Megill, p. 448. $)
  ax-11 $a |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $.

  $( Axiom of Quantifier Introduction.  One of the 5 equality axioms of
     predicate calculus.  Axiom scheme C9' of Megill, p. 448. $)
  ax-12 $a |- ( -. A. z z = x -> ( -. A. z z = y ->
              ( x = y -> A. z x = y ) ) ) $.

