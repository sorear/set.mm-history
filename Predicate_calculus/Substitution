$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Substitution
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c [ $. $( Left bracket $)
  $c / $. $( Division. $)
  $c ] $.  $( Right bracket $)

  $( Extend wff definition to include proper substitution (read "the wff that
     results when ` y ` is properly substituted for ` x ` in wff ` ph ` "). $)
  wsb $a wff [ y / x ] ph $.

  $( Define proper substitution.  Remark 9.1 in [Megill] p. 447 (p. 15 of the
     preprint).  For our notation, we use ` [ y / x ] ph ` to mean "the wff
     that results when ` y ` is properly substituted for ` x ` in the wff
     ` ph ` ." This notation was introduced in Haskell B. Curry's
     _Foundations of Mathematical Logic_ (1977), p. 316 and is frequently
     used in textbooks of lambda calculus and combinatory logic.  This
     notation improves the common but ambiguous notation, " ` ph ( y ) ` is
     the wff that results when ` y ` is properly substituted for ` x ` in
     ` ph ( x ) ` ". It is ambiguous because if we then properly substitute
     ` x ` for ` y ` in the resulting ` ph ( y ) ` , we do not always end up
     with the original ` ph ( x ) ` , for example when the original
     ` ph ( x ) ` is ` x = y ` . So what exactly does ` ph ( x ) ` mean?
     Curry's notation solves this problem.

     In most books, proper substitution has a somewhat complicated recursive
     definition with multiple cases based on the occurrences of free and
     bound variables in the wff.  Instead we use a remarkable little formula
     that is exactly equivalent and gives us a single direct definition.  We
     later prove that our definition has the properties we expect of proper
     substitution (see theorems ~ sbequ , ~ sbcom2 and ~ sbid2 ).

     Note that our definition is valid even when ` x ` and ` y ` are replaced
     with the same variable, as ~ sbid shows.  We achieve this by having ` x `
     free in the first conjunct and bound in the second.  We can also achieve
     this by using a dummy variable, as the alternate definition ~ sb7 shows
     (which some logicians may prefer because it doesn't mix free and bound
     variables).  When ` x ` and ` y ` are distinct, we can express proper
     substitution with the simpler expressions of ~ sb5 and ~ sb6 .

     There are no restrictions on what variables may occur in ` ph ` . $)
  df-sb $a |- ( [ y / x ] ph <->
              ( ( x = y -> ph ) /\ E. x ( x = y /\ ph ) ) ) $.

  ${
    sbim.1 $e |- ( ph -> ps ) $.
    $( Infer substitution into antecedent and consequent of an implication. $)
    sbim   $p |- ( [ y / x ] ph -> [ y / x ] ps ) $=
      ( weq wi wa wex wsb syl3 imlan 19.22i im2an df-sb 3imtr4 ) CDFZAGZQAHZCIZ
      HQBGZQBHZCIZHACDJBCDJRUATUCABQEKSUBCABQELMNACDOBCDOP $.
      $([25-Jun-98]$)
  $}

  ${
    bisb.1 $e |- ( ph <-> ps ) $.
    $( Infer substitution into both sides of a logical equivalence. $)
    bisb   $p |- ( [ y / x ] ph <-> [ y / x ] ps ) $=
      ( wsb biimp sbim biimpr impbi ) ACDFBCDFABCDABEGHBACDABEIHJ $.
      $([5-Aug-93]$)
  $}

  $( A distinctor elimination lemma for substitution. $)
  del43 $p |- ( A. x x = y -> ( [ z / x ] ph -> [ z / y ] ph ) ) $=
    ( weq wal wi wa wex wsb del25 eq4s syl4d imrand del40 del39 df-sb 3imtr4g
    ) BCEBFZBDEZAGZTAHZBIZHCDEZAGZUDAHZCIZHABDJACDJUAUEUCUGBCSUDTAUDTGCBCBDKLMU
    BUFBCSTUDABCDKNOPABDQACDQR $.
    $([5-Aug-93]$)

  $( A distinctor elimination lemma for substitution. $)
  del43b $p |- ( A. x x = y -> ( [ z / x ] ph <-> [ z / y ] ph ) ) $=
    ( weq wal wsb del43 wi eq4s impbid ) BCEBFABDGZACDGZABCDHMLICBACBDHJK $.
    $([5-Aug-93]$)

  $( One direction of a simplified definition for substitution. $)
  sb1 $p |- ( [ y / x ] ph -> E. x ( x = y /\ ph ) ) $=
    ( wsb weq wi wa wex df-sb pm3.27bd ) ABCDBCEZAFKAGBHABCIJ $.
    $([5-Aug-93]$)

  $( One direction of a simplified definition for substitution. $)
  sb2 $p |- ( A. x ( x = y -> ph ) -> [ y / x ] ph ) $=
    ( weq wi wal wa wex wsb ax-4 eqs4 jca df-sb sylibr ) BCDZAEZBFZPOAGBHZGABCI
    QPRPBJABCKLABCMN $.
    $([5-Aug-93]$)

  $( One direction of a simplified definition for substitution when variables
     are distinct. $)
  sb3 $p |- ( -. A. x x = y -> ( E. x ( x = y /\ ph ) -> [ y / x ] ph ) ) $=
    ( weq wal wn wa wex wi wsb eqs5 sb2 syl6 ) BCDZBEFNAGBHNAIBEABCJABCKABCLM
    $.
    $([5-Aug-93]$)

  $( One direction of a simplified definition for substitution when variables
     are distinct. $)
  sb4 $p |- ( -. A. x x = y -> ( [ y / x ] ph -> A. x ( x = y -> ph ) ) ) $=
    ( weq wal wn wa wex wi wsb eqs5 sb1 syl5 ) BCDZBEFNAGBHNAIBEABCJABCKABCLM
    $.
    $([5-Aug-93]$)

  $( Simplified definition for substitution when variables are distinct. $)
  sb4b $p |- ( -. A. x x = y -> ( [ y / x ] ph <-> A. x ( x = y -> ph ) ) ) $=
    ( weq wal wn wsb wi sb4 sb2 a1i impbid ) BCDZBEFZABCGZMAHBEZABCIPOHNABCJKL
    $.
    $([27-May-97]$)

  $( An equality theorem for substitution. $)
  sbequ1 $p |- ( x = y -> ( ph -> [ y / x ] ph ) ) $=
    ( weq wsb wa wi wex pm3.4 19.8a jca df-sb sylibr exp ) BCDZAABCEZOAFZOAGZQB
    HZFPQRSOAIQBJKABCLMN $.
    $([5-Aug-93]$)

  $( An equality theorem for substitution. $)
  sbequ2 $p |- ( x = y -> ( [ y / x ] ph -> ph ) ) $=
    ( weq wi wa wex wsb pm3.26 comm df-sb syl5ib ) BCDZMAEZMAFBGZFZAABCHPMANOIJ
    ABCKL $.
    $([5-Aug-93]$)

  $( An equality theorem for substitution. $)
  sbequ12 $p |- ( x = y -> ( ph <-> [ y / x ] ph ) ) $=
    ( weq wsb sbequ1 sbequ2 impbid ) BCDAABCEABCFABCGH $.
    $([5-Aug-93]$)

  $( An equality theorem for substitution. $)
  sbequ12a $p |- ( x = y -> ( [ y / x ] ph <-> [ x / y ] ph ) ) $=
    ( weq wsb sbequ12 wb eqcoms bitr3d ) BCDAABCEACBEZABCFAJGCBACBFHI $.
    $([5-Aug-93]$)

  $( An identity theorem for substitution.  Remark 9.1 in [Megill] p. 447
     (p. 15 of the preprint). $)
  sbid $p |- ( [ x / x ] ph <-> ph ) $=
    ( wsb weq wb eqid sbequ12 ax-mp bicom ) AABBCZBBDAJEBFABBGHI $.
    $([8-Jan-04]$)

  $( The specialization axiom of standard predicate calculus.  Axiom 4 of
     [Mendelson] p. 59. $)
  stdpc4 $p |- ( A. x ph -> [ y / x ] ph ) $=
    ( wal weq wi wsb ax-1 19.20i sb2 syl ) ABDBCEZAFZBDABCGAMBALHIABCJK $.
    $([5-Aug-93]$)

  ${
    sbf1.1 $e |- ( ph -> A. x ph ) $.
    $( Substitution of a variable not free does not affect a wff. $)
    sbf1   $p |- ( ph <-> [ y / x ] ph ) $=
      ( wsb wal stdpc4 syl weq wex wa sb1 19.41 sylib pm3.27d impbi ) AABCEZAAB
      FQDABCGHQBCIZBJZAQRAKBJSAKABCLRABDMNOP $.
      $([5-Aug-93]$)
  $}

  ${
    sb6x.1 $e |- ( ph -> A. x ph ) $.
    $( Equivalence involving substitution of a variable not free. $)
    sb6x   $p |- ( [ y / x ] ph <-> A. x ( x = y -> ph ) ) $=
      ( wsb weq wi wal sbf1 ax-1 19.21ai sylbir sb2 impbi ) ABCEZBCFZAGZBHZOARA
      BCDIAQBDAPJKLABCMN $.
      $([5-Aug-93]$)
  $}

  ${
    sb6y.1 $e |- ( ph -> A. y ph ) $.
    $( Equivalence involving substitution with a variable not free. $)
    sb6y  $p |- ( [ y / x ] ph <-> A. x ( x = y -> ph ) ) $=
      ( wsb weq wi wal sbequ2 a4s ax-10 eq4s ax-1 19.20i syl6 syl5 syld sb4
      pm2.61i sb2 impbi ) ABCEZBCFZAGZBHZUCBHZUBUEGUFUBAUEUCUBAGBABCIJUFACHZUEA
      UFUGABHZUEUGUHGCBACBKLAUDBAUCMNODPQABCRSABCTUA $.
      $([5-Aug-93]$)
  $}

  $( Substitution with a distinct variable makes the substituted variable not
     free. $)
  hbsb2 $p |- ( -. A. x x = y -> ( [ y / x ] ph -> A. x [ y / x ] ph ) ) $=
    ( weq wal wn wsb wi sb4 sb2 a5i syl6 ) BCDZBEFABCGZMAHZBENBEABCIONBABCJKL
    $.
    $([5-Aug-93]$)

  ${
    hbs1f.1 $e |- ( ph -> A. x ph ) $.
    $( If ` x ` is not free in ` ph ` , it is not free in ` [ y / x ] ph ` . $)
    hbs1f $p |- ( [ y / x ] ph -> A. x [ y / x ] ph ) $=
      ( wsb wal weq wex wa sb1 19.41 sylib pm3.27d stdpc4 a5i 3syl ) ABCEZAABFQ
      BFQBCGZBHZAQRAIBHSAIABCJRABDKLMDAQBABCNOP $.
      $([5-Aug-93]$)
  $}

  ${
    hbsb3.1 $e |- ( ph -> A. y ph ) $.
    $( If ` y ` is not free in ` ph ` , ` x ` is not free in
       ` [ y / x ] ph ` . $)
    hbsb3 $p |- ( [ y / x ] ph -> A. x [ y / x ] ph ) $=
      ( weq wal wsb wi sbequ2 a4s ax-10 eq4s syl5 syld sbequ1 19.20ii hbsb2
      pm2.61i ) BCEZBFZABCGZUABFZHTUAABFZUBTUAAUCSUAAHBABCIJTACFZUCAUDUCHCBACBK
      LDMNSAUABABCOPNABCQR $.
      $([5-Aug-93]$)
  $}

  ${
    hbsb4.1 $e |- ( ph -> A. z ph ) $.
    $( A variable not free remains so after substitution with a distinct
       variable. $)
    hbsb4 $p |- ( -. A. z z = y -> ( [ y / x ] ph -> A. z [ y / x ] ph ) )
              $=
      ( weq wal wn wsb wi del25 eq4s del35 del32 hbsb2 ax-10 syl9r syld wa eq5
      ax-4 19.20i sbequ2 a4s sbequ1 19.20ii syl5 3syl a1d sb4 eq6 hban ax-12
      imp a1i hbimd 19.20d sb2 a7s syl6 syl9 pm2.61i exp ) DBFDGZDCFZDGZHZABCIZ
      VHDGZJZJVDVGBCFZBGZHZVJVLVFDBVKVEDBVKVEJBDBDCKLMNVMVHVHBGZVDVIABCOVNVIJBD
      VHBDPLQRVDHZVGVJVLVOVGSZVJJVLVJVPVLVLDGVKDGZVJBCDTVLVKDVKBUAUBVQVHAVIVKVH
      AJDABCUCUDVQADGZVIAVKAVHDABCUEUFEUGRUHUIVMVHVKAJZBGZVPVIABCUJVPVTVSDGZBGV
      IVPVSWABVOVGBDBBUKDCBUKULVPVKADVOVGDDBDUKDCDUKULVOVGVKVQJBCDUMUNAVRJVPEUO
      UPUQVSVIDBVTVHDABCURUBUSUTVAVBVCVB $.
      $([5-Aug-93]$)
  $}

  $( An equality theorem for substitution. $)
  sbequi $p |- ( x = y -> ( [ x / z ] ph -> [ y / z ] ph ) ) $=
    ( weq wal wsb wi wn wa wex hbsb2 eqvin.l1 sbequ2 eqcoms sbequ1 sylan9
    19.22i syl 19.35 sylib eq6 19.9d syl9 exp com23 a4s adantr del43 eq4s
    sylan9r syld pm2.61ii ) DBEZDFZDCEZDFZBCEZADBGZADCGZHZHUOIZURUQIZVAVBURVCVA
    HVBURJUSUTDKZVCUTVBUSUSDFZURVDADBLURVADKZVEVDHURBDEZUPJZDKVFBCDMVHVADVGUSAU
    PUTUSAHZDBADBNZOADCPZQRSUSUTDTUAQUTVCDDCDUBADCLUCUDUEUFUOURVAUOURJUSAUTUOVI
    URUNVIDVJUGUHURAABCGZUOUTABCPVLUTHBDABDCUIUJUKULUEUQURVAUQURJUSAUTUQUSACBGZ
    URAADCBUIVMAHCBACBNOQUQAUTHZURUPVNDVKUGUHULUEUM $.
    $([5-Aug-93]$)

  $( An equality theorem for substitution.  Used in proof of Theorem 9.7
     in [Megill] p. 449 (p. 16 of the preprint). $)
  sbequ $p |- ( x = y -> ( [ x / z ] ph <-> [ y / z ] ph ) ) $=
    ( weq wsb sbequi wi eqcoms impbid ) BCEADBFZADCFZABCDGLKHCBACBDGIJ $.
    $([5-Aug-93]$)

  $( A distinctor elimination lemma for substitution. $)
  del44 $p |- ( A. x x = y -> ( [ x / z ] ph -> [ y / z ] ph ) ) $=
    ( weq wsb wi sbequi a4s ) BCEADBFADCFGBABCDHI $.
    $([5-Aug-93]$)

  $( A distinctor elimination lemma for substitution. $)
  del45 $p |- ( A. x x = y -> ( [ y / z ] ph -> [ x / z ] ph ) ) $=
    ( weq wsb wi sbequi eqcoms a4s ) BCEADCFADBFGZBKCBACBDHIJ $.
    $([5-Aug-93]$)

  $( Removal of negation from substitution. $)
  sbn1 $p |- ( [ y / x ] -. ph -> -. [ y / x ] ph ) $=
    ( weq wal wn wsb wi sbequ2 pm2.16d syld a4s sb4 wa wex sb1 eqs3 sylib con2
    syl6 pm2.61i ) BCDZBEZAFZBCGZABCGZFZHZUBUHBUBUEUDUGUDBCIUBUFAABCIJKLUCFUEUB
    UDHBEZUGUDBCMUFUIUFUBANBOUIFABCPABCQRSTUA $.
    $([7-Aug-94]$)

  $( Introduction of negation into substitution. $)
  sbn2 $p |- ( -. [ y / x ] ph -> [ y / x ] -. ph ) $=
    ( wsb wn weq wi wa wex sbequ1 pm2.16d comm wal sb2 pm4.13 bisb sylibr con3
    eqs3 jca df-sb ) ABCDZEZBCFZAEZGZUDUEHBIZHUEBCDUCUFUGUDUCUEUDAUBABCJKLUCUDU
    EEZGBMZEUGUIUBUIUHBCDUBUHBCNAUHBCAOPQRUEBCSQTUEBCUAQ $.
    $([7-Aug-94]$)

  $( Negation inside and outside of substitution are equivalent. $)
  sbn $p |- ( [ y / x ] -. ph <-> -. [ y / x ] ph ) $=
    ( wn wsb sbn1 sbn2 impbi ) ADBCEABCEDABCFABCGH $.
    $([5-Aug-93]$)

  ${
    sb5y.1 $e |- ( ph -> A. y ph ) $.
    $( Equivalence involving substitution with a variable not free. $)
    sb5y  $p |- ( [ y / x ] ph <-> E. x ( x = y /\ ph ) ) $=
      ( wsb weq wn wi wal wa wex hbne sb6y sbn bitr3 bicon2 eqs3 bitr4 ) ABCEZB
      CFZAGZHBIZGTAJBKUBSUBUABCESGUABCACDLMABCNOPABCQR $.
      $([5-Aug-93]$)
  $}

  $( Removal of implication from substitution. $)
  sbi1 $p |- ( [ y / x ] ( ph -> ps ) -> ( [ y / x ] ph -> [ y / x ] ps ) ) $=
    ( weq wal wi wsb sbequ2 syl5d sbequ1 syl6d a4s wn sb4 ax-2 19.20ii sb2
    syl6 pm2.61i ) CDEZCFZABGZCDHZACDHZBCDHZGGZUAUGCUAUDUEBUFUAUDABUEUCCDIACDIJ
    BCDKLMUBNZUDUAAGZCFZUFUEUHUDUAUCGZCFZUJUFGUCCDOULUJUABGZCFUFUKUIUMCUAABPQBC
    DRSSACDOJT $.
    $([5-Aug-93]$)

  $( Introduction of implication into substitution. $)
  sbi2 $p |- ( ( [ y / x ] ph -> [ y / x ] ps ) -> [ y / x ] ( ph -> ps ) ) $=
    ( wsb wi wn sbn pm2.21 sbim sylbir ax-1 ja ) ACDEZBCDEABFZCDEZNGAGZCDEPACDH
    QOCDABIJKBOCDBALJM $.
    $([5-Aug-93]$)

  $( Implication inside and outside of substitution are equivalent. $)
  sbi $p |- ( [ y / x ] ( ph -> ps ) <-> ( [ y / x ] ph -> [ y / x ] ps ) ) $=
    ( wi wsb sbi1 sbi2 impbi ) ABECDFACDFBCDFEABCDGABCDHI $.
    $([5-Aug-93]$)

  $( Logical OR inside and outside of substitution are equivalent. $)
  sbo $p |- ( [ y / x ] ( ph \/ ps ) <-> ( [ y / x ] ph \/ [ y / x ] ps ) ) $=
    ( wn wi wsb wo sbi sbn birim bitr df-or bisb 3bitr4 ) AEZBFZCDGZACDGZEZBCDG
    ZFZABHZCDGSUAHRPCDGZUAFUBPBCDIUDTUAACDJKLUCQCDABMNSUAMO $.
    $([29-Sep-02]$)

  ${
    sb19.21.1 $e |- ( ph -> A. x ph ) $.
    $( Substitution with a variable not free in antecedent affects only the
       consequent. $)
    sb19.21   $p |- ( [ y / x ] ( ph -> ps ) <-> ( ph -> [ y / x ] ps ) ) $=
      ( wi wsb sbi sbf1 birim bitr4 ) ABFCDGACDGZBCDGZFAMFABCDHALMACDEIJK $.
      $([5-Aug-93]$)
  $}

  $( Conjunction inside and outside of a substitution are equivalent. $)
  sba $p |- ( [ y / x ] ( ph /\ ps ) <-> ( [ y / x ] ph /\ [ y / x ] ps ) ) $=
    ( wn wi wsb wa sbn sbi bilim bitr bineg df-an bisb 3bitr4 ) ABEZFZEZCDGZACD
    GZBCDGZEZFZEZABHZCDGUAUBHTRCDGZEUERCDIUGUDUGUAQCDGZFUDAQCDJUHUCUABCDIKLMLUF
    SCDABNOUAUBNP $.
    $([5-Aug-93]$)

  $( Equivalence inside and outside of a substitution are equivalent. $)
  sbb $p |- ( [ y / x ] ( ph <-> ps ) <-> ( [ y / x ] ph <-> [ y / x ] ps ) )
            $=
    ( wb wsb wi wa bi bisb sbi bi2an sba 3bitr4 bitr ) ABEZCDFABGZBAGZHZCDFZACD
    FZBCDFZEZPSCDABIJQCDFZRCDFZHUAUBGZUBUAGZHTUCUDUFUEUGABCDKBACDKLQRCDMUAUBINO
    $.
    $([5-Aug-93]$)

  ${
    sblbis.1 $e |- ( [ y / x ] ph <-> ps ) $.
    $( Introduce left biconditional inside of a substitution. $)
    sblbis   $p |- ( [ y / x ] ( ch <-> ph ) <-> ( [ y / x ] ch <-> ps ) ) $=
      ( wb wsb sbb bilbi bitr ) CAGDEHCDEHZADEHZGLBGCADEIMBLFJK $.
      $([19-Aug-93]$)
  $}

  ${
    sbrbis.1 $e |- ( [ y / x ] ph <-> ps ) $.
    $( Introduce right biconditional inside of a substitution. $)
    sbrbis   $p |- ( [ y / x ] ( ph <-> ch ) <-> ( ps <-> [ y / x ] ch ) ) $=
      ( wb wsb sbb birbi bitr ) ACGDEHADEHZCDEHZGBMGACDEILBMFJK $.
      $([18-Aug-93]$)
  $}

  ${
    sbrbif.1 $e |- ( ch -> A. x ch ) $.
    sbrbif.2 $e |- ( [ y / x ] ph <-> ps ) $.
    $( Introduce right biconditional inside of a substitution. $)
    sbrbif   $p |- ( [ y / x ] ( ph <-> ch ) <-> ( ps <-> ch ) ) $=
      ( wb wsb sbrbis sbf1 bilbi bitr4 ) ACHDEIBCDEIZHBCHABCDEGJCNBCDEFKLM $.
      $([18-Aug-93]$)
  $}

  $( A specialization theorem. $)
  sbea4 $p |- ( [ y / x ] ph -> E. x ph ) $=
    ( wsb wn wal wex stdpc4 sbn sylib con2 df-ex sylibr ) ABCDZAEZBFZEABGPNPOBC
    DNEOBCHABCIJKABLM $.
    $([5-Aug-93]$)

  $( Specialization of implication. $)
  sbia4 $p |- ( A. x ( ph -> ps ) -> ( [ y / x ] ph -> [ y / x ] ps ) ) $=
    ( wi wal wsb stdpc4 sbi sylib ) ABEZCFKCDGACDGBCDGEKCDHABCDIJ $.
    $([5-Aug-93]$)

  $( Specialization of biconditional. $)
  sbba4 $p |- ( A. x ( ph <-> ps ) -> ( [ y / x ] ph <-> [ y / x ] ps ) ) $=
    ( wb wal wsb stdpc4 sbb sylib ) ABEZCFKCDGACDGBCDGEKCDHABCDIJ $.
    $([5-Aug-93]$)

  ${
    bisbd.1 $e |- ( ph -> A. x ph ) $.
    bisbd.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Deduction substituting both sides of a biconditional. $)
    bisbd   $p |- ( ph -> ( [ y / x ] ps <-> [ y / x ] ch ) ) $=
      ( wb wal wsb 19.21ai sbba4 syl ) ABCHZDIBDEJCDEJHANDFGKBCDELM $.
      $([5-Aug-93]$)
  $}

  $( Substitution does not change a complemented distinctor. $)
  sbequ5 $p |- ( A. x x = y <-> [ w / z ] A. x x = y ) $=
    ( weq wal eq5 sbf1 ) ABEAFCDABCGH $.
    $([5-Aug-93]$)

  $( Substitution does not change a distinctor. $)
  sbequ6 $p |- ( -. A. x x = y <-> [ w / z ] -. A. x x = y ) $=
    ( weq wal wn eq6 sbf1 ) ABEAFGCDABCHI $.
    $([5-Aug-93]$)

  ${
    sbt.1 $e |- ph $.
    $( A substitution into a theorem remains true. $)
    sbt $p |- [ y / x ] ph $=
      ( weq wi wsb sb2 a1i mpg ) BCEZAFABCGBABCHAKDIJ $.
      $([21-Jan-04]$)
  $}

  $( Substitution applied to atomic wff. $)
  sbeq1 $p |- [ y / x ] x = y $=
    ( weq wi wsb sb2 id mpg ) ABCZIDIABEAIABFIGH $.
    $([5-Aug-93]$)

  $( Substitution applied to atomic wff. $)
  sbeq2 $p |- [ y / x ] y = x $=
    ( weq wi wsb sb2 eqcom mpg ) ABCBACZDIABEAIABFABGH $.
    $([5-Aug-93]$)

  $( Elimination of equality from antecedent after substitution. $)
  sbequ8 $p |- ( [ y / x ] ph <-> [ y / x ] ( x = y -> ph ) ) $=
    ( wsb weq wi sbeq1 a1bi sbi bitr4 ) ABCDZBCEZBCDZKFLAFBCDMKBCGHLABCIJ $.
    $([5-Aug-93]$)

  ${
    ddel1.1 $e |- ( ph -> A. x ph ) $.
    ddel1.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    ddel1.3 $e |- ( ph -> ( x = y -> ( ps -> ch ) ) ) $.
    $( Conversion of implicit substitution to explicit substitution. $)
    ddel1   $p |- ( ph -> ( [ y / x ] ps -> ch ) ) $=
      ( wal wsb wi wex weq wa imp3a 19.20i 19.22 syl sb1 syl5 hba1 19.23 sylib
      ax-4 syl6 syld ) AADIZBDEJZCKFUGUHCDLZCUGDEMZBNZDLZUIUHUGUKCKZDIULUIKAUMD
      AUJBCHOPUKCDQRBDESTUGUICDIZCUGCUNKZDIUIUNKAUODGPCUNDCDUAUBUCCDUDUEUFR $.
      $([30-Jun-94]$)
  $}

  ${
    ddel2.1 $e |- ( ph -> A. x ph ) $.
    ddel2.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    ddel2.3 $e |- ( ph -> ( x = y -> ( ch -> ps ) ) ) $.
    $( Conversion of implicit substitution to explicit substitution. $)
    ddel2   $p |- ( ph -> ( ch -> [ y / x ] ps ) ) $=
      ( wal wsb wi a4s weq com23 19.20ii sb2 syl6 syld syl ) AADIZCBDEJZKFTCCDI
      ZUAACUBKDGLTUBDEMZBKZDIUAACUDDAUCCBHNOBDEPQRS $.
      $([30-Jun-94]$)
  $}

  ${
    ddel3.1 $e |- ( ph -> A. x ph ) $.
    ddel3.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    ddel3.3 $e |- ( ph -> ( x = y -> ( ps <-> ch ) ) ) $.
    $( Conversion of implicit substitution to explicit substitution. $)
    ddel3   $p |- ( ph -> ( [ y / x ] ps <-> ch ) ) $=
      ( wsb weq wb wi bi1 syl6 ddel1 bi2 ddel2 impbid ) ABDEICABCDEFGADEJZBCKZB
      CLHBCMNOABCDEFGASTCBLHBCPNQR $.
      $([30-Jun-94]$)
  $}

  ${
    ddel4.1 $e |- ( ps -> A. x ps ) $.
    ddel4.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Conversion of implicit substitution to explicit substitution. $)
    ddel4   $p |- ( [ y / x ] ph <-> ps ) $=
      ( wi wsb wb id hbth wal a1i weq ddel3 ax-mp ) AAGZACDHBIAJZQABCDQCRKBBCLG
      QEMCDNABIGQFMOP $.
      $([30-Jun-94]$)
  $}

  ${
    ddelimf.1 $e |- ( z = y -> ( ph <-> ps ) ) $.
    ddelimf.2 $e |- ( ph -> A. x ph ) $.
    ddelimf.3 $e |- ( ps -> A. z ps ) $.
    $( Version of ~ ddelim without any variable restrictions. $)
    ddelimf $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( weq wal wn wsb hbsb4 ddel4 bial 3imtr3g ) CDICJKAEDLZQCJBBCJAEDCGMABEDH
      FNZQBCROP $.
      $([1-Oct-02]$)
  $}

  ${
    ddelimf2.1 $e |- ( z = y -> ( ph <-> ps ) ) $.
    ddelimf2.2 $e |- ( ph -> A. x ph ) $.
    ddelimf2.3 $e |- ( ps -> A. z ps ) $.
    $( Proof of ~ ddelimf without using ~ ax-11 . $)
    ddelimf2 $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( weq wal wn wi ax-10 eq4s hba1 syl5 a1d wa eq6 hban ax-12 imp a1i hbimd
      hbald exp pm2.61i eqsal bial 3imtr3g ) CDICJKZEDIZALZEJZUNCJZBBCJCEICJZUK
      UNUOLZLUPUQUKUPUNEJZUOUNURUOLECUNECMNUMEOPQUPKZUKUQUSUKRZUMCEUSUKECEESCDE
      STUTULACUSUKCCECSCDCSTUSUKULULCJLEDCUAUBAACJLUTGUCUDUEUFUGABEDHFUHZUNBCVA
      UIUJ $.
      $([12-Nov-02]$)
  $}

  $( A composition law for substitution. $)
  sbco  $p |- ( [ y / x ] [ x / y ] ph <-> [ y / x ] ph ) $=
    ( wsb wb weq sbeq2 sbequ12 bicomd sbim ax-mp sbb mpbi ) ACBDZAEZBCDZNBCDABC
    DECBFZBCDPBCGQOBCQANACBHIJKNABCLM $.
    $([11-Jan-04]$)

  ${
    sbco0.1 $e |- ( ph -> A. x ph ) $.
    $( A composition law for substitution. $)
    sbco0   $p |- ( [ y / x ] [ x / y ] ph <-> ph ) $=
      ( wsb sbco sbf1 bitr4 ) ACBEBCEABCEAABCFABCDGH $.
      $([11-Jan-04]$)
  $}

  $( An idempotent law for substitution. $)
  sbidm  $p |- ( [ y / x ] [ y / x ] ph <-> [ y / x ] ph ) $=
    ( weq wal wsb wb sbequ12 bicomd a4s wn eq6 hbsb2 wi pm4.2i a1i ddel3
    pm2.61i ) BCDZBEZABCFZBCFZUAGZSUCBSUAUBUABCHIJTKZUAUABCBCBLABCMSUAUAGNUDSUA
    OPQR $.
    $([27-Jan-04]$)

  ${
    sbco2.1 $e |- ( ph -> A. z ph ) $.
    $( A composition law for substitution. $)
    sbco2   $p |- ( [ y / z ] [ z / x ] ph <-> [ y / x ] ph ) $=
      ( weq wal wsb wb sbequ sbco0 syl5bbr sbequ12 bitr3d a4s wn eq6 hbsb3
      hbsb4 wi a1i ddel3 bicomd pm2.61i ) BCFZBGZABDHZDCHZABCHZIZUEUJBUEAUHUIUE
      UGDBHUHAUGBCDJADBEKLZABCMNOUFPZUIUHULAUHBCBCBQUGDCBABDERSUEAUHITULUKUAUBU
      CUD $.
      $([11-Jan-04]$)
  $}

  ${
    sbco2d.1 $e |- ( ph -> A. x ph ) $.
    sbco2d.2 $e |- ( ph -> A. z ph ) $.
    sbco2d.3 $e |- ( ph -> ( ps -> A. z ps ) ) $.
    $( A composition law for substitution. $)
    sbco2d   $p |- ( ph -> ( [ y / z ] [ z / x ] ps <-> [ y / x ] ps ) ) $=
      ( wsb wi hbim1 sbco2 sb19.21 bisb bitr 3bitr3 pm5.74ri ) ABCEIZEDIZBCD
      IZABJZCEIZEDIZUACDIASJZATJUACDEABEGHKLUCARJZEDIUDUBUEEDABCEFMNAREDGMOABCD
      FMPQ $.
      $([11-Jan-04]$)
  $}

  $( A composition law for substitution. $)
  sbco3 $p |- ( [ z / y ] [ y / x ] ph <-> [ z / x ] [ x / y ] ph ) $=
    ( weq wal wsb wb del43b sbequ12a 19.20i sbba4 syl bitr3d wn eq6 hbsb2
    sbco2d sbco bisb syl5rbbr pm2.61i ) BCEZBFZABCGZCDGZACBGZBDGZHUDUEBDGZUFUHU
    EBCDIUDUEUGHZBFUIUHHUCUJBABCJKUEUGBDLMNUDOZUECBGZBDGUFUHUKUECDBBCCPBCBPABCQ
    RULUGBDACBSTUAUB $.
    $([5-Aug-93]$)

  $( A commutativity law for substitution. $)
  sbcom $p |- ( [ y / z ] [ y / x ] ph <-> [ y / x ] [ y / z ] ph ) $=
    ( weq wal wsb wb wn wa wi del43b eq5 bisbd bitr3d a1d eq6 hban ax-12 imp
    19.20i 19.21g 3syl biald adantrr eq4ds bi2.04 bial syl5bb 19.5 adantrl
    sb4b bilimd sylan9bbr adantl sylan9bb 3bitr4d exp pm2.61i sbequ12 a4s
    pm2.61ii ) BCEZBFZDCEZDFZABCGZDCGZADCGZBCGZHZVDIZVFIZVKBDEBFZVLVMJZVKKVNVKV
    OVNVGBCGVHVJVGBDCLVNVGVIBCBDBMABDCLNOPVNIZVOVKVPVOJZVEVCAKZBFZKZDFZVCVEAKZD
    FZKZBFZVHVJVQVEVRKZBFZDFZWAWEVPVLWHWAHVMVPVLJZWGVTDVPVLDBDDQZBCDQZRWIWIBFVE
    VEBFKZBFWGVTHVPVLBBDBQZBCBQRWIWLBVPVLWLDCBSTUAVEVRBUBUCUDUEVPVMWHWEHVLVPVMJ
    ZWFDFZBFWEWHWNWOWDBVPVMBWMDCBQZRWNVCWBKZDFZWDWOWNWNDFVCVCDFKZDFWRWDHVPVMDWJ
    DCDQRWNWSDVPVMWSVMWSKDBBCDSUFTUAVCWBDUBUCWFWQDVEVCAUGUHUIUDWFDBUJUIUKOVOVHW
    AHVPVMVHVEVGKZDFVLWAVGDCULVLWTVTDWKVLVGVSVEABCULUMUDUNUOVOVJWEHVPVLVJVCVIKZ
    BFVMWEVIBCULVMXAWDBWPVMVIWCVCADCULUMUDUPUOUQURUSURVDVIVHVJVDAVGDCBCDMVCAVGH
    BABCUTVANVCVIVJHBVIBCUTVAOVFVGVHVJVEVGVHHDVGDCUTVAVFAVIBCDCBMVEAVIHDADCUTVA
    NOVB $.
    $([27-May-97]$)

  ${
    sb5f1.1 $e |- ( ph -> A. x ph ) $.
    $( Equivalence for substitution.  Similar to Theorem 6.2 of [Quine]
       p. 40. $)
    sb5f1   $p |- ( ph <-> A. x ( x = y -> [ x / y ] ph ) ) $=
      ( weq wsb wi wal sbequ1 eqcoms comm 19.21ai sb2 sbco sylib sbf1
      sylibr impbi ) ABCEZACBFZGZBHZAUABDSATATGCBACBIJKLUBABCFZAUBTBCFUCTBCMABC
      NOABCDPQR $.
      $([5-Aug-93]$)
  $}

  ${
    sb8.1 $e |- ( ph -> A. y ph ) $.
    $( Substitution of variable in universal quantifier. $)
    sb8   $p |- ( A. x ph <-> A. y [ y / x ] ph ) $=
      ( wal wsb hbal stdpc4 19.21ai hbsb3 sbco0 sylib impbi ) ABEZABCFZCEZNO
      CACBDGABCHIPABOBCABCDJGPOCBFAOCBHACBDKLIM $.
      $([5-Aug-93]$)
  $}

  ${
    sb8e.1 $e |- ( ph -> A. y ph ) $.
    $( Substitution of variable in existential quantifier. $)
    sb8e   $p |- ( E. x ph <-> E. y [ y / x ] ph ) $=
      ( wn wal wsb wex hbne sb8 sbn bial bitr bineg df-ex 3bitr4 ) AEZBFZEABCGZ
      EZCFZEABHSCHRUARQBCGZCFUAQBCACDIJUBTCABCKLMNABOSCOP $.
      $([12-Aug-93]$)
  $}

  $( Commutation of quantification and substitution variables. $)
  sb9i $p |- ( A. x [ x / y ] ph -> A. y [ y / x ] ph ) $=
    ( weq wal wsb wi del43 del45 syld del35 wn hbsb2 19.20ii eq6s stdpc4
    sbco sylib 19.20i a7s syl6 pm2.61i ) CBDCEZACBFZBEZABCFZCEZGUDUFCBUCUDAB
    BFUFACBBHACBBIJKUCLZUEUDCEZBEZUGUEUJGCBBUHUDUIBACBMNOUDUGCBUEUFCUEUDBCFUFUD
    BCPABCQRSTUAUB $.
    $([5-Aug-93]$)

  $( Commutation of quantification and substitution variables. $)
  sb9 $p |- ( A. x [ x / y ] ph <-> A. y [ y / x ] ph ) $=
    ( wsb wal sb9i impbi ) ACBDBEABCDCEABCFACBFG $.
    $([5-Aug-93]$)



