$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Substitution
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$(
   Now we come to the concept of proper substitution.  Logic texts
   usually introduce substitution as a new primitive notion described
   informally, such as "replace all free occurrences of variable ` x ` with
   variable ` y `, renaming bound variables as needed."  The technical
   details of the standard definition are somewhat complex and
   effectively introduce additional underlying axioms.  Rather than
   introduce a new primitive notion, we will define substitution using the
   existing language.  Then we will show that our definition has the
   properties we expect of substitution.

   For our notation, we use ` ( ph x | y ) ` to mean "the wff that results
   when ` y ` is properly substituted for ` x ` in the wff ` ph `."  The
   placement of parentheses is slightly different than in the notation
   ` ph ( x | y ) ` that is sometimes used, because the latter notation is
   ambiguous for us:  for example, we don't know whether ` -. ph ( x | y ) ` is
   to be interpreted as ` -. ( ph x | y ) ` or ` ( -. ph x | y ) `.  (Because
   of the way we have defined ` wff `, this is the case with all "postfix"
   wff operators.)  Some authors use the notation ` [ y / x ] ph `, which
   would also work for us without ambiguity.
$)

  $( Declare a new symbol first used by the substitution notation. $)
  $c | $.  $( Vertical bar $)

  $( Extend wff definition to include proper substitution (read "the wff that
     results when ` y ` is properly substituted for ` x ` in wff ` ph `"). $)
  wsb $a wff ( ph x | y ) $.

  $( Define proper substitution.  Note that this definition is valid even
     when ` x ` and ` y ` are replaced with the same variable.  The first
     conjunct is a "trick" used to achieve this property, making the
     definition look somewhat counterintutive at first.  Remark 9.1 of Megill,
     p. 447. $)
  df-sb $a |- ( ( ph x | y ) <->
              ( ( x = y -> ph ) /\ E. x ( x = y /\ ph ) ) ) $.

  ${
    sbim.1 $e |- ( ph -> ps ) $.
    $( Infer substitution into antecedent and consequent of an implication. $)
    sbim   $p |- ( ( ph x | y ) -> ( ps x | y ) ) $=
      ( weq wi wa wex wsb syl3 imlan 19.22i im2an df-sb 3imtr4 ) CDFZAGZQAHZCIZ
      HQBGZQBHZCIZHACDJBCDJRUATUCABQEKSUBCABQELMNACDOBCDOP $.
  $}

  ${
    bisb.1 $e |- ( ph <-> ps ) $.
    $( Infer substitution into both sides of a logical equivalence. $)
    bisb   $p |- ( ( ph x | y ) <-> ( ps x | y ) ) $=
      ( wsb biimp sbim biimpr impbi ) ACDFBCDFABCDABEGHBACDABEIHJ $.
  $}

  $( A distinctor elimination lemma for substitution. $)
  del43 $p |- ( A. x x = y -> ( ( ph x | z ) -> ( ph y | z ) ) ) $=
      ( weq wal wi wa wex wsb del25 eq4s syl4d imrand del40 del39 df-sb
      3imtr4g ) BCEBFZBDEZAGZTAHZBIZHCDEZAGZUDAHZCIZHABDJACDJUAUEUCUGBCSUDTAUDT
      GCBCBDKLMUBUFBCSTUDABCDKNOPABDQACDQR $.

  $( A distinctor elimination lemma for substitution. $)
  del43b $p |- ( A. x x = y -> ( ( ph x | z ) <-> ( ph y | z ) ) ) $=
      ( weq wal wsb del43 wi eq4s impbid ) BCEBFABDGZACDGZABCDHMLICBACBDHJK $.

  $( One direction of a simplified definition for substitution. $)
  sb1 $p |- ( ( ph x | y ) -> E. x ( x = y /\ ph ) ) $=
      ( wsb weq wi wa wex df-sb pm3.27 sylbi ) ABCDBCEZAFZLAGBHZGNABCIMNJK $.

  $( One direction of a simplified definition for substitution. $)
  sb2 $p |- ( A. x ( x = y -> ph ) -> ( ph x | y ) ) $=
      ( weq wi wal wa wex wsb ax-4 eqs4 jca df-sb sylibr ) BCDZAEZBFZPOAGBHZGAB
      CIQPRPBJABCKLABCMN $.

  $( One direction of a simplified definition for substitution when variables
     are distinct. $)
  sb3 $p |- ( -. A. x x = y -> ( E. x ( x = y /\ ph ) -> ( ph x | y ) ) ) $=
      ( weq wal wn wa wex wi wsb eqs5 sb2 syl6 ) BCDZBEFNAGBHNAIBEABCJABCKABCLM
      $.

  $( One direction of a simplified definition for substitution when variables
     are distinct. $)
  sb4 $p |- ( -. A. x x = y -> ( ( ph x | y ) -> A. x ( x = y -> ph ) ) ) $=
      ( weq wal wn wa wex wi wsb eqs5 sb1 syl5 ) BCDZBEFNAGBHNAIBEABCJABCKABCLM
      $.

  $( Simplified definition for substitution when variables are distinct. $)
  sb4b $p |- ( -. A. x x = y -> ( ( ph x | y ) <-> A. x ( x = y -> ph ) ) ) $=
      ( weq wal wn wsb wi sb4 sb2 a1i impbid ) BCDZBEFZABCGZMAHBEZABCIPOHNABCJK
      L $.
      $([27-May-97]$)

  $( An equality theorem for substitution. $)
  sbequ1 $p |- ( x = y -> ( ph -> ( ph x | y ) ) ) $=
      ( weq wsb wa wi wex pm3.4 19.8a jca df-sb sylibr exp ) BCDZAABCEZOAFZOAGZ
      QBHZFPQRSOAIQBJKABCLMN $.

  $( An equality theorem for substitution. $)
  sbequ2 $p |- ( x = y -> ( ( ph x | y ) -> ph ) ) $=
      ( weq wi wa wex wsb pm3.26 comm df-sb syl5ib ) BCDZMAEZMAFBGZFZAABCHPMANO
      IJABCKL $.

  $( An equality theorem for substitution. $)
  sbequ12 $p |- ( x = y -> ( ph <-> ( ph x | y ) ) ) $=
      ( weq wsb sbequ1 sbequ2 impbid ) BCDAABCEABCFABCGH $.

  $( An equality theorem for substitution. $)
  sbequ12a $p |- ( x = y -> ( ( ph x | y ) <-> ( ph y | x ) ) ) $=
      ( weq wsb sbequ12 wb eqcoms bitr3d ) BCDAABCEACBEZABCFAJGCBACBFHI $.

  $( An identity theorem for substitution.  Remark 9.1 of Megill, p. 447. $)
  sbid $p |- ( ph <-> ( ph x | x ) ) $=
      ( weq wsb wb sbequ12 eqid ax-mp ) BBCAABBDEABBFBGH $.

  $( The specialization axiom of standard predicate calculus.  Axiom 4 of
     Mendelson, p. 59. $)
  stdpc4 $p |- ( A. x ph -> ( ph x | y ) ) $=
      ( wal weq wi wsb ax-1 19.20i sb2 syl ) ABDBCEZAFZBDABCGAMBALHIABCJK $.

  ${
    sbf1.1 $e |- ( ph -> A. x ph ) $.
    $( Substitution of a variable not free does not affect a wff. $)
    sbf1   $p |- ( ph <-> ( ph x | y ) ) $=
      ( wsb wal stdpc4 syl weq wex wa sb1 19.41 sylib pm3.27d impbi ) AABCEZAA
      BFQDABCGHQBCIZBJZAQRAKBJSAKABCLRABDMNOP $.
  $}

  ${
    sb6x.1 $e |- ( ph -> A. x ph ) $.
    $( Equivalence involving substitution of a variable not free. $)
    sb6x   $p |- ( ( ph x | y ) <-> A. x ( x = y -> ph ) ) $=
      ( wsb weq wi wal sbf1 ax-1 19.21ai sylbir sb2 impbi ) ABCEZBCFZAGZBHZOARA
      BCDIAQBDAPJKLABCMN $.
  $}

  ${
    sb6y.1 $e |- ( ph -> A. y ph ) $.
    $( Equivalence involving substitution with a variable not free. $)
    sb6y  $p |- ( ( ph x | y ) <-> A. x ( x = y -> ph ) ) $=
      ( wsb weq wi wal sbequ2 a4s ax-10 eq4s ax-1 19.20i syl6 syl5 syld sb4
      pm2.61i sb2 impbi ) ABCEZBCFZAGZBHZUCBHZUBUEGUFUBAUEUCUBAGBABCIJUFACHZUEA
      UFUGABHZUEUGUHGCBACBKLAUDBAUCMNODPQABCRSABCTUA $.
  $}

  $( Substitution with a distinct variable makes the substituted variable not
     free. $)
  hbsb $p |- ( -. A. x x = y -> ( ( ph x | y ) -> A. x ( ph x | y ) ) ) $=
      ( weq wal wn wsb wi sb4 sb2 a5i syl6 ) BCDZBEFABCGZMAHZBENBEABCIONBABCJKL
      $.

  ${
    hbsba.1 $e |- ( ph -> A. x ph ) $.
    $( If ` x ` is not free in ` ph `, it is not free in ` ( ph x | y ) `. $)
    hbsba   $p |- ( ( ph x | y ) -> A. x ( ph x | y ) ) $=
      ( wsb wal weq wex wa sb1 19.41 sylib pm3.27d stdpc4 a5i 3syl ) ABCEZAABF
      QBFQBCGZBHZAQRAIBHSAIABCJRABDKLMDAQBABCNOP $.
  $}

  ${
    hbsbb.1 $e |- ( ph -> A. y ph ) $.
    $( If ` y ` is not free in ` ph `, ` x ` is not free in
       ` ( ph x | y ) `. $)
    hbsbb   $p |- ( ( ph x | y ) -> A. x ( ph x | y ) ) $=
      ( weq wal wsb wi sbequ2 a4s ax-10 eq4s syl5 syld sbequ1 19.20ii hbsb
      pm2.61i ) BCEZBFZABCGZUABFZHTUAABFZUBTUAAUCSUAAHBABCIJTACFZUCAUDUCHCBACBK
      LDMNSAUABABCOPNABCQR $.
  $}

  ${
    hbsbc.1 $e |- ( ph -> A. z ph ) $.
    $( A variable not free remains so after substitution with a distinct
       variable. $)
    hbsbc $p |- ( -. A. z z = y -> ( ( ph x | y ) -> A. z ( ph x | y ) ) )
              $=
      ( weq wal wn wsb wi del25 eq4s del35 del32 hbsb ax-10 syl9r syld wa
      ax-12 imp comm a4s sbequ2 sbequ1 19.20ii syl5 syl6 sb4 eq6 hban a1i
      hbimd syl sb2 19.20i a7s syl9 pm2.61i exp ) DBFDGZDCFZDGZHZABCIZVEDGZJZ
      JVAVDBCFZBGZHZVGVIVCDBVHVBDBVHVBJBDBDCKLMNVJVEVEBGZVAVFABCOVKVFJBDVEBDPLQ
      RVAHZVDVGVIVLVDSZVGJVIVMVHDGZVGVHVMVNJBVMVHVNVLVDVHVNJBCDTUAZUBUCVNVEAVFV
      HVEAJDABCUDUCVNADGZVFAVHAVEDABCUEUFEUGRUHVJVEVHAJZBGZVMVFABCUIVMVRVQDGZBG
      ZVFVMVMBGVRVTJVLVDBDBBUJDCBUJUKVMVQVSBVMVMDGVQVSJVLVDDDBDUJDCDUJUKVMVHADV
      OAVPJVMEULUMUNUFUNVQVFDBVRVEDABCUOUPUQUHURUSUTUS $.
  $}

  $( An equality theorem for substitution. $)
  sbequi $p |- ( x = y -> ( ( ph z | x ) -> ( ph z | y ) ) ) $=
      ( weq wal wsb wi wn wa wex hbsb eqvin.l1a sbequ2 eqcoms sbequ1 sylan9
      19.22i syl 19.35 sylib eq6 19.9d syl9 exp com23 a4s adantr del43 eq4s
      sylan9r syld pm2.61ii ) DBEZDFZDCEZDFZBCEZADBGZADCGZHZHUOIZURUQIZVAVBURV
      CVAHVBURJUSUTDKZVCUTVBUSUSDFZURVDADBLURVADKZVEVDHURBDEZUPJZDKVFBCDMVHVADV
      GUSAUPUTUSAHZDBADBNZOADCPZQRSUSUTDTUAQUTVCDDCDUBADCLUCUDUEUFUOURVAUOURJUS
      AUTUOVIURUNVIDVJUGUHURAABCGZUOUTABCPVLUTHBDABDCUIUJUKULUEUQURVAUQURJUSAUT
      UQUSACBGZURAADCBUIVMAHCBACBNOQUQAUTHZURUPVNDVKUGUHULUEUM $.

  $( An equality theorem for substitution.  Used in proof of Theorem 9.7
     of Megill, p. 449. $)
  sbequ $p |- ( x = y -> ( ( ph z | x ) <-> ( ph z | y ) ) ) $=
      ( weq wsb sbequi wi eqcoms impbid ) BCEADBFZADCFZABCDGLKHCBACBDGIJ $.

  $( A distinctor elimination lemma for substitution. $)
  del44 $p |- ( A. x x = y -> ( ( ph z | x ) -> ( ph z | y ) ) ) $=
      ( weq wsb wi sbequi a4s ) BCEADBFADCFGBABCDHI $.

  $( A distinctor elimination lemma for substitution. $)
  del45 $p |- ( A. x x = y -> ( ( ph z | y ) -> ( ph z | x ) ) ) $=
      ( weq wsb wi sbequi eqcoms a4s ) BCEADCFADBFGZBKCBACBDHIJ $.

  $( Removal of negation from substitution. $)
  sbn1 $p |- ( ( -. ph x | y ) -> -. ( ph x | y ) ) $=
      ( weq wal wn wsb wi sbequ2 pm2.16d syld a4s sb4 wa wex sb1 eqs3 sylib
      con2 syl6 pm2.61i ) BCDZBEZAFZBCGZABCGZFZHZUBUHBUBUEUDUGUDBCIUBUFAABCIJKL
      UCFUEUBUDHBEZUGUDBCMUFUIUFUBANBOUIFABCPABCQRSTUA $.
      $([7-Aug-94]$)

  $( Introduction of negation into substitution. $)
  sbn2 $p |- ( -. ( ph x | y ) -> ( -. ph x | y ) ) $=
      ( wsb wn weq wi wa wex sbequ1 pm2.16d comm wal sb2 pm4.13 bisb sylibr
      con3 eqs3 jca df-sb ) ABCDZEZBCFZAEZGZUDUEHBIZHUEBCDUCUFUGUDUCUEUDAUBABCJ
      KLUCUDUEEZGBMZEUGUIUBUIUHBCDUBUHBCNAUHBCAOPQRUEBCSQTUEBCUAQ $.
      $([7-Aug-94]$)

  $( Negation inside and outside of substitution are equivalent. $)
  sbn $p |- ( ( -. ph x | y ) <-> -. ( ph x | y ) ) $=
      ( wn wsb sbn1 sbn2 impbi ) ADBCEABCEDABCFABCGH $.

  ${
    sb5y.1 $e |- ( ph -> A. y ph ) $.
    $( Equivalence involving substitution with a variable not free. $)
    sb5y  $p |- ( ( ph x | y ) <-> E. x ( x = y /\ ph ) ) $=
      ( wsb weq wn wi wal wa wex hbne sb6y sbn bitr3 bicon2 eqs3 bitr4 ) ABCEZB
      CFZAGZHBIZGTAJBKUBSUBUABCESGUABCACDLMABCNOPABCQR $.
  $}

  $( Removal of implication from substitution. $)
  sbi1 $p |- ( ( ( ph -> ps ) x | y ) -> ( ( ph x | y ) -> ( ps x | y ) ) ) $=
      ( weq wal wi wsb sbequ2 syl5d sbequ1 syl6d a4s wn sb4 ax-2 19.20ii sb2
      syl6 pm2.61i ) CDEZCFZABGZCDHZACDHZBCDHZGGZUAUGCUAUDUEBUFUAUDABUEUCCDIACD
      IJBCDKLMUBNZUDUAAGZCFZUFUEUHUDUAUCGZCFZUJUFGUCCDOULUJUABGZCFUFUKUIUMCUAAB
      PQBCDRSSACDOJT $.

  $( Introduction of implication into substitution. $)
  sbi2 $p |- ( ( ( ph x | y ) -> ( ps x | y ) ) -> ( ( ph -> ps ) x | y ) ) $=
      ( wsb wi wn sbn pm2.21 sbim sylbir ax-1 ja ) ACDEZBCDEABFZCDEZNGAGZCDEPAC
      DHQOCDABIJKBOCDBALJM $.

  $( Implication inside and outside of substitution are equivalent. $)
  sbi $p |- ( ( ( ph -> ps ) x | y ) <-> ( ( ph x | y ) -> ( ps x | y ) ) ) $=
      ( wi wsb sbi1 sbi2 impbi ) ABECDFACDFBCDFEABCDGABCDHI $.

  ${
    sb19.21.1 $e |- ( ph -> A. x ph ) $.
    $( Substitution with a variable not free in antecedent affects only the
       consequent. $)
    sb19.21   $p |- ( ( ( ph -> ps ) x | y ) <-> ( ph -> ( ps x | y ) ) ) $=
      ( wi wsb sbi sbf1 birim bitr4 ) ABFCDGACDGZBCDGZFAMFABCDHALMACDEIJK $.
  $}

  $( Conjunction inside and outside of a substitution are equivalent. $)
  sba $p |- ( ( ( ph /\ ps ) x | y ) <-> ( ( ph x | y ) /\ ( ps x | y ) ) ) $=
      ( wn wi wsb wa sbn sbi bilim bitr bineg df-an bisb 3bitr4 ) ABEZFZEZCDGZA
      CDGZBCDGZEZFZEZABHZCDGUAUBHTRCDGZEUERCDIUGUDUGUAQCDGZFUDAQCDJUHUCUABCDIKL
      MLUFSCDABNOUAUBNP $.

  $( Equivalence inside and outside of a substitution are equivalent. $)
  sbb $p |- ( ( ( ph <-> ps ) x | y ) <-> ( ( ph x | y ) <-> ( ps x | y ) ) )
            $=
      ( wb wsb wi wa bi bisb sbi bi2an sba 3bitr4 bitr ) ABEZCDFABGZBAGZHZCDFZA
      CDFZBCDFZEZPSCDABIJQCDFZRCDFZHUAUBGZUBUAGZHTUCUDUFUEUGABCDKBACDKLQRCDMUAU
      BINO $.

  ${
    sblbis.1 $e |- ( ( ph x | y ) <-> ps ) $.
    $( Introduce left biconditional inside of a substitution. $)
    sblbis   $p |- ( ( ( ch <-> ph ) x | y ) <-> ( ( ch x | y ) <-> ps ) ) $=
      ( wb wsb sbb bilbi bitr ) CAGDEHCDEHZADEHZGLBGCADEIMBLFJK $.
      $([19-Aug-93]$)
  $}

  ${
    sbrbis.1 $e |- ( ( ph x | y ) <-> ps ) $.
    $( Introduce right biconditional inside of a substitution. $)
    sbrbis   $p |- ( ( ( ph <-> ch ) x | y ) <-> ( ps <-> ( ch x | y ) ) ) $=
      ( wb wsb sbb birbi bitr ) ACGDEHADEHZCDEHZGBMGACDEILBMFJK $.
      $([18-Aug-93]$)
  $}

  ${
    sbrbif.1 $e |- ( ch -> A. x ch ) $.
    sbrbif.2 $e |- ( ( ph x | y ) <-> ps ) $.
    $( Introduce right biconditional inside of a substitution. $)
    sbrbif   $p |- ( ( ( ph <-> ch ) x | y ) <-> ( ps <-> ch ) ) $=
      ( wb wsb sbrbis sbf1 bilbi bitr4 ) ACHDEIBCDEIZHBCHABCDEGJCNBCDEFKLM $.
      $([18-Aug-93]$)
  $}

  $( A specialization theorem. $)
  sbea4 $p |- ( ( ph x | y ) -> E. x ph ) $=
      ( wsb wn wal wex stdpc4 sbn sylib con2 df-ex sylibr ) ABCDZAEZBFZEABGPNPO
      BCDNEOBCHABCIJKABLM $.

  $( Specialization of implication. $)
  sbia4 $p |- ( A. x ( ph -> ps ) -> ( ( ph x | y ) -> ( ps x | y ) ) ) $=
      ( wi wal wsb stdpc4 sbi sylib ) ABEZCFKCDGACDGBCDGEKCDHABCDIJ $.

  $( Specialization of biconditional. $)
  sbba4 $p |- ( A. x ( ph <-> ps ) -> ( ( ph x | y ) <-> ( ps x | y ) ) ) $=
      ( wb wal wsb stdpc4 sbb sylib ) ABEZCFKCDGACDGBCDGEKCDHABCDIJ $.

  ${
    bisbd.1 $e |- ( ph -> A. x ph ) $.
    bisbd.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Deduction substituting both sides of a biconditional. $)
    bisbd   $p |- ( ph -> ( ( ps x | y ) <-> ( ch x | y ) ) ) $=
      ( wb wal wsb 19.21ai sbba4 syl ) ABCHZDIBDEJCDEJHANDFGKBCDELM $.
  $}

  $( Substitution does not change a complemented distinctor. $)
  sbequ5 $p |- ( A. x x = y <-> ( A. x x = y z | w ) ) $=
      ( weq wal eq5 sbf1 ) ABEAFCDABCGH $.

  $( Substitution does not change a distinctor. $)
  sbequ6 $p |- ( -. A. x x = y <-> ( -. A. x x = y z | w ) ) $=
      ( weq wal wn eq6 sbf1 ) ABEAFGCDABCHI $.

  $( Substitution applied to atomic wff. $)
  sbeq1 $p |- ( x = x x | y ) $=
      ( weq wi wsb sb2 eqid a1i mpg ) ABCZAACZDKABEAKABFKJAGHI $.

  $( Substitution applied to atomic wff. $)
  sbeq2 $p |- ( x = y x | y ) $=
      ( weq wi wsb sb2 id mpg ) ABCZIDIABEAIABFIGH $.

  $( Substitution applied to atomic wff. $)
  sbeq3 $p |- ( y = x x | y ) $=
      ( weq wi wsb sb2 eqcom mpg ) ABCBACZDIABEAIABFABGH $.

  $( Substitution applied to atomic wff. $)
  sbeq4 $p |- ( y = y x | y ) $=
      ( weq wi wsb sb2 eqid a1i mpg ) ABCZBBCZDKABEAKABFKJBGHI $.

  $( Elimination of equality from antecedent after substitution. $)
  sbequ8 $p |- ( ( ph x | y ) <-> ( ( x = y -> ph ) x | y ) ) $=
      ( wsb weq wi sbeq2 a1bi sbi bitr4 ) ABCDZBCEZBCDZKFLAFBCDMKBCGHLABCIJ $.

  ${
    ddel1.1 $e |- ( ph -> A. x ph ) $.
    ddel1.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    ddel1.3 $e |- ( ph -> ( x = y -> ( ps -> ch ) ) ) $.
    $( Conversion of implicit substitution to explicit substitution. $)
    ddel1   $p |- ( ph -> ( ( ps x | y ) -> ch ) ) $=
      ( wal wsb wi wex weq wa imp3a 19.20i 19.22 syl sb1 syl5 hba1 19.23 sylib
      ax-4 syl6 syld ) AADIZBDEJZCKFUGUHCDLZCUGDEMZBNZDLZUIUHUGUKCKZDIULUIKAUM
      DAUJBCHOPUKCDQRBDESTUGUICDIZCUGCUNKZDIUIUNKAUODGPCUNDCDUAUBUCCDUDUEUFR $.
      $([30-Jun-94]$)
  $}

  ${
    ddel2.1 $e |- ( ph -> A. x ph ) $.
    ddel2.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    ddel2.3 $e |- ( ph -> ( x = y -> ( ch -> ps ) ) ) $.
    $( Conversion of implicit substitution to explicit substitution. $)
    ddel2   $p |- ( ph -> ( ch -> ( ps x | y ) ) ) $=
      ( wal wsb wi a4s weq com23 19.20ii sb2 syl6 syld syl ) AADIZCBDEJZKFTCCD
      IZUAACUBKDGLTUBDEMZBKZDIUAACUDDAUCCBHNOBDEPQRS $.
      $([30-Jun-94]$)
  $}

  ${
    ddel3.1 $e |- ( ph -> A. x ph ) $.
    ddel3.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    ddel3.3 $e |- ( ph -> ( x = y -> ( ps <-> ch ) ) ) $.
    $( Conversion of implicit substitution to explicit substitution. $)
    ddel3   $p |- ( ph -> ( ( ps x | y ) <-> ch ) ) $=
      ( wsb weq wb wi df-bi1 syl6 ddel1 df-bi2 ddel2 impbid ) ABDEICABCDEFGADE
      JZBCKZBCLHBCMNOABCDEFGASTCBLHBCPNQR $.
      $([30-Jun-94]$)
  $}

  ${
    ddel4.1 $e |- ( ps -> A. x ps ) $.
    ddel4.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Conversion of implicit substitution to explicit substitution. $)
    ddel4   $p |- ( ( ph x | y ) <-> ps ) $=
      ( wi wsb wb id hbth wal a1i weq ddel3 ax-mp ) AAGZACDHBIQABCDQCAJZKBBCLGQ
      EMCDNABIGQFMORP $.
      $([30-Jun-94]$)
  $}

  $( A composition law for substitution. $)
  sbco  $p |- ( ( ph x | y ) <-> ( ( ph y | x ) x | y ) ) $=
      ( wsb wb sbb weq sbequ12 eqcoms sbim sbeq2 ax-mp mpbi ) AACBDZEZBCDZABCDN
      BCDEANBCFBCGZBCDPQOBCOCBACBHIJBCKLM $.

  ${
    sbco0.1 $e |- ( ph -> A. x ph ) $.
    $( A composition law for substitution. $)
    sbco0   $p |- ( ph <-> ( ( ph y | x ) x | y ) ) $=
      ( wsb sbf1 sbco bitr ) AABCEACBEBCEABCDFABCGH $.
  $}

  $( A composition law for substitution. $)
  sbco1  $p |- ( ( ph x | y ) <-> ( ( ph x | y ) x | y ) ) $=
      ( weq wal wsb wb sbequ12 a4s wn eq6 hbsb wi pm4.2i a1i ddel3 bicomd
      pm2.61i ) BCDZBEZABCFZUABCFZGZSUCBUABCHITJZUBUAUDUAUABCBCBKABCLSUAUAGMUDS
      UANOPQR $.
      $([30-Jun-94]$)

  ${
    sbco2.1 $e |- ( ph -> A. z ph ) $.
    $( A composition law for substitution. $)
    sbco2   $p |- ( ( ph x | y ) <-> ( ( ph x | z ) z | y ) ) $=
      ( wsb weq wal wb sbequ sbco0 syl5bb sbequ12 bitr3d a4s wn eq6 hbsbb
      hbsbc wi a1i ddel3 bicomd pm2.61i bicom ) ABDFZDCFZABCFZBCGZBHZUGUHIZUIU
      KBUIAUGUHUIUFDBFUGAUFBCDJADBEKLZABCMNOUJPZUHUGUMAUGBCBCBQUFDCBABDERSUIAUG
      ITUMULUAUBUCUDUE $.
      $([30-Jun-94]$)
  $}

  ${
    sbco2d.1 $e |- ( ph -> A. x ph ) $.
    sbco2d.2 $e |- ( ph -> A. z ph ) $.
    sbco2d.3 $e |- ( ph -> ( ps -> A. z ps ) ) $.
    $( A composition law for substitution. $)
    sbco2d   $p |- ( ph -> ( ( ps x | y ) <-> ( ( ps x | z ) z | y ) ) ) $=
      ( wsb wi hbim1 sbco2 sb19.21 bisb bitr 3bitr3 pm5.74ri ) ABCDIZBCEIZEDIZA
      BJZCDIUACEIZEDIZARJATJZUACDEABEGHKLABCDFMUCASJZEDIUDUBUEEDABCEFMNASEDGMOP
      Q $.
  $}

  $( A composition law for substitution. $)
  sbco3 $p |- ( ( ( ph x | y ) y | z ) <-> ( ( ph y | x ) x | z ) ) $=
      ( weq wal wsb wb del43b sbequ12a 19.20i sbba4 syl bitr3d wn eq6 hbsb
      sbco2d sbco bisb syl6bbr pm2.61i ) BCEZBFZABCGZCDGZACBGZBDGZHUDUEBDGZUFU
      HUEBCDIUDUEUGHZBFUIUHHUCUJBABCJKUEUGBDLMNUDOZUFUECBGZBDGUHUKUECDBBCCPBCBP
      ABCQRUGULBDACBSTUAUB $.

  $( A composition commutation law for substitution. $)
  sbcoc $p |- ( ( ( ph x | y ) z | y ) <-> ( ( ph z | y ) x | y ) ) $=
      ( weq wal wsb wb wn wa wi del43b eq5 bisbd bitr3d a1d eq6 hban ax-12 imp
      19.20i 19.21g 3syl biald adantarr eq4 con3 syl bi2.04 bial syl5bb 19.5
      adantarl sb4b bilimd sylan9rbb adantl sylan9bb 3bitr4d exp pm2.61i
      sbequ12 a4s pm2.61ii ) BCEZBFZDCEZDFZABCGZDCGZADCGZBCGZHZVFIZVHIZVMBDEBFZ
      VNVOJZVMKVPVMVQVPVIBCGVJVLVIBDCLVPVIVKBCBDBMABDCLNOPVPIZVQVMVRVQJZVGVEAKZ
      BFZKZDFZVEVGAKZDFZKZBFZVJVLVSVGVTKZBFZDFZWCWGVRVNWJWCHVOVRVNJZWIWBDVRVNDB
      DDQZBCDQZRWKWKBFVGVGBFKZBFWIWBHVRVNBBDBQZBCBQRWKWNBVRVNWNDCBSTUAVGVTBUBUC
      UDUEVRVOWJWGHVNVRVOJZWHDFZBFWGWJWPWQWFBVRVOBWODCBQZRWPVEWDKZDFZWFWQWPWPDF
      VEVEDFKZDFWTWFHVRVODWLDCDQRWPXADVRVOXAVRDBEDFZIVOXAKXBVPDBUFUGBCDSUHTUAVE
      WDDUBUCWHWSDVGVEAUIUJUKUDWHDBULUKUMOVQVJWCHVRVOVJVGVIKZDFVNWCVIDCUNVNXCWB
      DWMVNVIWAVGABCUNUOUDUPUQVQVLWGHVRVNVLVEVKKZBFVOWGVKBCUNVOXDWFBWRVOVKWEVEA
      DCUNUOUDURUQUSUTVAUTVFVKVJVLVFAVIDCBCDMVEAVIHBABCVBVCNVEVKVLHBVKBCVBVCOVH
      VIVJVLVGVIVJHDVIDCVBVCVHAVKBCDCBMVGAVKHDADCVBVCNOVD $.
      $([27-May-97]$)

  ${
    sb5f1.1 $e |- ( ph -> A. x ph ) $.
    $( Equivalence for substitution.  Similar to Theorem 6.2 of Quine,
       p. 40. $)
    sb5f1   $p |- ( ph <-> A. x ( x = y -> ( ph y | x ) ) ) $=
      ( weq wsb wi wal sbequ1 eqcoms comm 19.21ai sb2 sbco sylibr sbf1 impbi )
      ABCEZACBFZGZBHZATBDRASASGCBACBIJKLUAABCFZAUASBCFUBSBCMABCNOABCDPOQ $.
  $}

  ${
    sb8.1 $e |- ( ph -> A. y ph ) $. $!??? swap bicond.?
    $( Substitution of variable in universal quantifier. $)
    sb8   $p |- ( A. x ph <-> A. y ( ph x | y ) ) $=
      ( wal wsb hbal stdpc4 19.21ai hbsbb sbco0 sylibr impbi ) ABEZABCFZCEZNOCA
      CBDGABCHIPABOBCABCDJGPOCBFAOCBHACBDKLIM $.
  $}

  ${
    sb8e.1 $e |- ( ph -> A. y ph ) $. $!??? swap bicond.?
    $( Substitution of variable in existential quantifier. $)
    sb8e   $p |- ( E. x ph <-> E. y ( ph x | y ) ) $=
      ( wn wal wsb wex hbne sb8 sbn bial bitr bineg df-ex 3bitr4 ) AEZBFZEABCGZ
      EZCFZEABHSCHRUARQBCGZCFUAQBCACDIJUBTCABCKLMNABOSCOP $.
      $([12-Aug-93]$)
  $}

  $( Commutation of quantification and substitution variables. $)
  sb9i $p |- ( A. x ( ph y | x ) -> A. y ( ph x | y ) ) $=
      ( weq wal wsb wi del43 del45 syld del35 wn hbsb 19.20ii eq6s stdpc4
      sbco sylibr 19.20i a7s syl6 pm2.61i ) CBDCEZACBFZBEZABCFZCEZGUDUFCBUCUDAB
      BFUFACBBHACBBIJKUCLZUEUDCEZBEZUGUEUJGCBBUHUDUIBACBMNOUDUGCBUEUFCUEUDBCFUF
      UDBCPABCQRSTUAUB $.

  $( Commutation of quantification and substitution variables. $)
  sb9 $p |- ( A. x ( ph y | x ) <-> A. y ( ph x | y ) ) $=
      ( wsb wal sb9i impbi ) ACBDBEABCDCEABCFACBFG $.


