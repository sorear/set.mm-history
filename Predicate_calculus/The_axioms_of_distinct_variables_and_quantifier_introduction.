$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The axioms of distinct variables and quantifier introduction.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Axiom of Distinct Variables.  The only axiom of predicate calculus
       requiring that variables be distinct (if we consider ~ ax-17 below to be
       a metatheorem and not an axiom).  Axiom scheme C16' in [Megill]
       p. 448 (p. 16 of the preprint).    It apparently does not otherwise
       appear in the literature but is easily proved from textbook predicate
       calculus by cases.  It is a somewhat bizarre axiom since the antecedent
       is always false in set theory (see ~ dtru ) but nonetheless technically
       necessary as you can see from its uses. $)
    ax-16 $a |- ( A. x x = y -> ( ph -> A. x ph ) ) $.
  $}



  ${
    $d x z $. $d y z $.
    $( Theorem to add distinct quantifier to atomic formula. $)
    ax17eq $p |- ( x = y -> A. z x = y ) $=
      ( weq wal wi ax-12 ax-16 pm2.61ii ) CADCECBDCEABDZJCEFABCGJCAHJCBHI $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x z $. $d y z $.
    $( Theorem to add distinct quantifier to atomic formula.  (This theorem
       demonstrates the induction basis for ~ ax-17 considered as a
       metatheorem. Do not use it for later proofs - use ~ ax-17 instead, to
       avoid reference to the redundant ~ ax-15 .) $)
    ax17el $p |- ( x e. y -> A. z x e. y ) $=
      ( weq wal wel wi ax-15 ax-16 pm2.61ii ) CADCECBDCEABFZKCEGABCHKCAIKCBIJ
      $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ph $.
    $( Axiom to quantify a variable over a formula in which it does not occur.
       Axiom C5 in [Megill] p. 444 (p. 11 of the preprint).  Also appears as
       Axiom B6 (p. 75) of system S2 of [Tarski] p. 77.  If we allow ~ ax-15
       (not otherwise used in our development), this axiom is logically
       redundant since it is a metatheorem justified by induction on the number
       of primitive connectives in wff ` ph ` , using ~ ax17eq and ~ ax17el
       together ~ hbne , ~ hbal , and ~ hbim . However if we omit this axiom
       our development would be quite inconvenient since we could work only
       with specific instances of wffs containing no wff variables - this axiom
       is effectively a definition of the concept of a set variable not
       occurring in a wff (as opposed to just two set variables being
       distinct).  $)
    ax-17 $a |- ( ph -> A. x ph ) $.
  $}

  ${
    $d x y $.
    $( This is a version of ~ ax-11 when the variables are distinct.
       Axiom (C8) of [Monk2] p. 105. $)
    ax11a $p |- ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) $=
      ( weq wal wi ax-16 ax-1 syl5 a1d ax-11 pm2.61i ) BCDZBEZMAMAFZBEZFZFNQMNO
      PAOBCGAMHIJABCKL $.
      $( [5-Aug-93] $)
  $}

  ${
           $d x ps $.
    a4b.1 $e |- ( x = y -> ( ph -> ps ) ) $.
    $( A weaker version of ~ a4a . $)
    a4b $p |- ( A. x ph -> ps ) $=
      ( ax-17 a4a ) ABCDBCFEG $.
      $( [5-Aug-93] $)
  $}

  ${
           $d x ps $.
    a4b1.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Infer specializaton rule from substitution instance. $)
    a4b1 $p |- ( A. x ph -> ps ) $=
      ( weq biimpd a4b ) ABCDCDFABEGH $.
      $( [30-Aug-93] $)
  $}

  ${
    $d x ph $.
    a4w.1 $e |- ( x = y -> ( ph -> ps ) ) $.
    $( A weaker version of ~ a4c . $)
    a4w $p |- ( ph -> E. x ps ) $=
      ( ax-17 a4c ) ABCDACFEG $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ps $.
    a4w1.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    a4w1.2 $e |- ps $.
    $( Infer existence from a substitution instance. $)
    a4w1 $p |- E. x ph $=
      ( wex weq biimprd a4w ax-mp ) BACGFBACDCDHABEIJK $.
      $( [19-Aug-93] $)
  $}

  ${
    $d x z $.  $d y z $.
    $( A variable elimination law for equality.  Lemma 15 of [Monk2] p. 109. $)
    eqvin.l2 $p |- ( E. z ( x = z /\ z = y ) -> x = y ) $=
      ( weq wa ax-17 eqt imp 19.23ai ) ACDZCBDZEABDZCLCFJKLACBGHI $.
      $( [5-Aug-93] $)

    $( A variable introduction law for equality.  Lemma 15 of [Monk2] p. 109. $)
    eqvin $p |- ( x = y <-> E. z ( x = z /\ z = y ) ) $=
      ( weq wa wex eqvin.l1 eqvin.l2 impbi ) ABDACDCBDECFABCGABCHI $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x y $.
    $( A generalization of axiom ~ ax-16 . $)
    a16g $p |- ( A. x x = y -> ( ph -> A. z ph ) ) $=
      ( weq wal wi eq5 wn ax9a ax-16 mt3i eqcom syl 19.21ai idd del35 syl9r
      mpcom ) DBEZDFZBCEBFZAADFZGUBTDBCDHUBBDEZTUBUDUDIZBFBDJUEBCKLBDMNOUBAABFU
      AUCABCKAADBUAAPQRS $.
      $( [5-Aug-93] $)

    $( A generalization of axiom ~ ax-16 . $)
    a16gb $p |- ( A. x x = y -> ( ph <-> A. z ph ) ) $=
      ( weq wal a16g wi ax-4 a1i impbid ) BCEBFZAADFZABCDGMAHLADIJK $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ph $.
    bialdv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for universal quantifier (deduction rule). $)
    bialdv $p |- ( ph -> ( A. x ps <-> A. x ch ) ) $=
      ( ax-17 biald ) ABCDADFEG $.
      $( [5-Aug-93] $)

    $( Formula-building rule for existential quantifier (deduction rule). $)
    biexdv $p |- ( ph -> ( E. x ps <-> E. x ch ) ) $=
      ( ax-17 biexd ) ABCDADFEG $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ph $.  $d y ph $.
    bi2aldv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for 2 existential quantifiers (deduction rule). $)
    bi2aldv $p |- ( ph -> ( A. x A. y ps <-> A. x A. y ch ) ) $=
      ( wal bialdv ) ABEGCEGDABCEFHH $.
      $( [4-Mar-97] $)

    $( Formula-building rule for 2 existential quantifiers (deduction rule). $)
    bi2exdv $p |- ( ph -> ( E. x E. y ps <-> E. x E. y ch ) ) $=
      ( wex biexdv ) ABEGCEGDABCEFHH $.
      $( [1-May-95] $)
  $}

  ${
    $d x ph $.  $d y ph $.  $d z ph $.
    bi3exdv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for 3 existential quantifiers (deduction rule). $)
    bi3exdv $p |- ( ph -> ( E. x E. y E. z ps <-> E. x E. y E. z ch ) ) $=
      ( wex biexdv bi2exdv ) ABFHCFHDEABCFGIJ $.
      $( [1-May-95] $)
  $}

  ${
    $d x ph $.  $d y ph $.  $d z ph $.  $d w ph $.
    bi4exdv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for 4 existential quantifiers (deduction rule). $)
    bi4exdv $p |- ( ph ->
                     ( E. x E. y E. z E. w ps <-> E. x E. y E. z E. w ch ) ) $=
      ( wex bi2exdv ) ABGIFICGIFIDEABCFGHJJ $.
      $( [3-Aug-95] $)
  $}

  ${
    $d x ph $.
    $( Special case of Theorem 19.9 of [Margaris] p. 89. $)
    19.9rv $p |- ( ph <-> E. x ph ) $=
      ( ax-17 19.9r ) ABABCD $.
      $( [28-May-95] $)
  $}

  ${
    $d x ph $.
    $( Special case of Theorem 19.21 of [Margaris] p. 90.  _Notational
       convention_:  We sometimes suffix with "v" the label of a theorem
       eliminating a hypothesis such as ` ( ph -> A. x ph ) ` in ~ 19.21
       via the use of distinct variable conditions combined with ~ ax-17 .
       Conversely, we sometimes suffix with "f" the label of a theorem
       introducing such a hypothesis to eliminate the need for the
       distinct variable condition; e.g. ~ euf derived from ~ df-eu .
       The "f" stands for "not free in" which is less restrictive than
       "does not occur in". $)
    19.21v $p |- ( A. x ( ph -> ps ) <-> ( ph -> A. x ps ) ) $=
      ( ax-17 19.21 ) ABCACDE $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ph $.
    19.21aiv.1 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90. $)
    19.21aiv $p |- ( ph -> A. x ps ) $=
      ( ax-17 19.21ai ) ABCACEDF $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ph $.  $d y ph $.
    19.21aivv.1 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90. $)
    19.21aivv $p |- ( ph -> A. x A. y ps ) $=
      ( wal 19.21aiv ) ABDFCABDEGG $.
      $( [31-Jul-95] $)
  $}

  ${
    $d x ph $.
    $d x ps $.
    19.21adv.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.21 of [Margaris] p. 90. $)
    19.21adv $p |- ( ph -> ( ps -> A. x ch ) ) $=
      ( ax-17 19.21ad ) ABCDADFBDFEG $.
      $( [10-Feb-97] $)
  $}

  ${
    $d x ph $.
    19.20dv.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.20 of [Margaris] p. 90. $)
    19.20dv $p |- ( ph -> ( A. x ps -> A. x ch ) ) $=
      ( ax-17 19.20d ) ABCDADFEG $.
      $( [3-Apr-94] $)

    $( Deduction from Theorem 19.22 of [Margaris] p. 90. $)
    19.22dv $p |- ( ph -> ( E. x ps -> E. x ch ) ) $=
      ( ax-17 19.22d ) ABCDADFEG $.
      $( [27-Apr-94] $)
  $}

  ${
    $d x ph $.  $d y ph $.
    19.20dvv.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.22 of [Margaris] p. 90. $)
    19.20dvv $p |- ( ph -> ( A. x A. y ps -> A. x A. y ch ) ) $=
      ( wal 19.20dv ) ABEGCEGDABCEFHH $.
      $( [8-May-04] $) $( [27-Apr-04] $)

    $( Deduction from Theorem 19.22 of [Margaris] p. 90. $)
    19.22dvv $p |- ( ph -> ( E. x E. y ps -> E. x E. y ch ) ) $=
      ( wex 19.22dv ) ABEGCEGDABCEFHH $.
      $( [3-Aug-95] $)
  $}

  ${
    $d x ps $.
    $( Special case of Theorem 19.23 of [Margaris] p. 90. $)
    19.23v $p |- ( A. x ( ph -> ps ) <-> ( E. x ph -> ps ) ) $=
      ( ax-17 19.23 ) ABCBCDE $.
      $( [28-Jun-98] $)
  $}

  ${
    $d x ps $.  $d y ps $.
    $( Theorem 19.23 of [Margaris] p. 90 extended to two variables. $)
    19.23vv $p |- ( A. x A. y ( ph -> ps ) <-> ( E. x E. y ph -> ps ) ) $=
      ( wi wal wex 19.23v bial bitr ) ABEDFZCFADGZBEZCFLCGBEKMCABDHILBCHJ $.
      $( [23-Aug-04] $) $( [10-Aug-04] $)
  $}

  ${
    $d x ps $.
    19.23aiv.1 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90. $)
    19.23aiv $p |- ( E. x ph -> ps ) $=
      ( ax-17 19.23ai ) ABCBCEDF $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ps $.  $d y ps $.
    19.23aivv.1 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90. $)
    19.23aivv $p |- ( E. x E. y ph -> ps ) $=
      ( wex 19.23aiv ) ADFBCABDEGG $.
      $( [1-Aug-95] $)
  $}

  ${
    $d x ch $.  $d x ph $.
    19.23adv.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.23 of [Margaris] p. 90. $)
    19.23adv $p |- ( ph -> ( E. x ps -> ch ) ) $=
      ( ax-17 19.23ad ) ABCDADFCDFEG $.
      $( [27-Apr-94] $)
  $}

  ${
    $d x ch $.  $d x ph $.  $d y ch $.  $d y ph $.
    19.23advv.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.23 of [Margaris] p. 90. $)
    19.23advv $p |- ( ph -> ( E. x E. y ps -> ch ) ) $=
      ( wex 19.23adv ) ABEGCDABCEFHH $.
      $( [31-Jul-95] $)
  $}

  ${
    $d x ps $.
    $( Theorem 19.27 of [Margaris] p. 90. $)
    19.27v $p |- ( A. x ( ph /\ ps ) <-> ( A. x ph /\ ps ) ) $=
      ( ax-17 19.27 ) ABCBCDE $.
      $( [4-Jun-04] $) $( [3-Jun-04] $)
  $}

  ${
    $d x ph $.
    $( Theorem 19.28 of [Margaris] p. 90. $)
    19.28v $p |- ( A. x ( ph /\ ps ) <-> ( ph /\ A. x ps ) ) $=
      ( ax-17 19.28 ) ABCACDE $.
      $( [25-Mar-04] $) $( [25-Mar-04] $)
  $}

  ${
    $d x ps $.
    $( Special case of Theorem 19.36 of [Margaris] p. 90. $)
    19.36v $p |- ( E. x ( ph -> ps ) <-> ( A. x ph -> ps ) ) $=
      ( ax-17 19.36 ) ABCBCDE $.
      $( [18-Aug-93] $)
  $}

  ${
    $d x ps $.
    19.36aiv.1 $e |- E. x ( ph -> ps ) $.
    $( Inference from Theorem 19.36 of [Margaris] p. 90. $)
    19.36aiv $p |- ( A. x ph -> ps ) $=
      ( ax-17 19.36i ) ABCBCEDF $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x y $.  $d x ps $.  $d y ph $.
    $( Special case of ~ 19.12 where its converse holds. $)
    19.12vv $p |- ( E. x A. y ( ph -> ps ) <-> A. y E. x ( ph -> ps ) ) $=
      ( wi wal wex 19.21v biex 19.36v bial bitr2 3bitr ) ABEZDFZCGABDFZEZCGACFZ
      PEZNCGZDFZOQCABDHIAPCJUARBEZDFSTUBDABCJKRBDHLM $.
      $( [18-Jul-01] $)
  $}

  ${
    $d x ph $.
    $( Special case of Theorem 19.37 of [Margaris] p. 90. $)
    19.37v $p |- ( E. x ( ph -> ps ) <-> ( ph -> E. x ps ) ) $=
      ( ax-17 19.37 ) ABCACDE $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ph $.
    19.37aiv.1 $e |- E. x ( ph -> ps ) $.
    $( Inference from Theorem 19.37 of [Margaris] p. 90. $)
    19.37aiv $p |- ( ph -> E. x ps ) $=
      ( wi wex 19.37v mpbi ) ABECFABCFEDABCGH $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ps $.
    $( Special case of Theorem 19.41 of [Margaris] p. 90. $)
    19.41v $p |- ( E. x ( ph /\ ps ) <-> ( E. x ph /\ ps ) ) $=
      ( ax-17 19.41 ) ABCBCDE $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ps $.  $d y ps $.
    $( Theorem 19.41 of [Margaris] p. 90 with 2 quantifiers. $)
    19.41vv $p |- ( E. x E. y ( ph /\ ps ) <-> ( E. x E. y ph /\ ps ) ) $=
      ( wa wex 19.41v biex bitr ) ABEDFZCFADFZBEZCFKCFBEJLCABDGHKBCGI $.
      $( [30-Apr-95] $)
  $}

  ${
    $d x ps $.  $d y ps $.  $d z ps $.
    $( Theorem 19.41 of [Margaris] p. 90 with 3 quantifiers. $)
    19.41vvv $p |- ( E. x E. y E. z ( ph /\ ps ) <->
                     ( E. x E. y E. z ph /\ ps ) ) $=
      ( wa wex 19.41vv biex 19.41v bitr ) ABFEGDGZCGAEGDGZBFZCGMCGBFLNCABDEHIMB
      CJK $.
      $( [30-Apr-95] $)
  $}

  ${
    $d x ph $.
    $( Special case of Theorem 19.42 of [Margaris] p. 90. $)
    19.42v $p |- ( E. x ( ph /\ ps ) <-> ( ph /\ E. x ps ) ) $=
      ( ax-17 19.42 ) ABCACDE $.
      $( [5-Aug-93] $)
  $}

  ${
    $d y ph $.
    $( Distribution of existential quantifiers. $)
    exdistr $p |- ( E. x E. y ( ph /\ ps ) <-> E. x ( ph /\ E. y ps ) ) $=
      ( wa wex 19.42v biex ) ABEDFABDFECABDGH $.
      $( [9-Mar-95] $)
  $}

  ${
    $d x ph $.  $d y ph $.
    $( Theorem 19.42 of [Margaris] p. 90 with 2 quantifiers. $)
    19.42vv $p |- ( E. x E. y ( ph /\ ps ) <-> ( ph /\ E. x E. y ps ) ) $=
      ( wa wex exdistr 19.42v bitr ) ABEDFCFABDFZECFAJCFEABCDGAJCHI $.
      $( [16-Mar-95] $)
  $}

  ${
    $d y ph $.  $d z ph $.
    $( Distribution of existential quantifiers. $)
    exdistr2 $p |- ( E. x E. y E. z ( ph /\ ps ) <->
                   E. x ( ph /\ E. y E. z ps ) ) $=
      ( wa wex 19.42vv biex ) ABFEGDGABEGDGFCABDEHI $.
      $( [17-Mar-95] $)
  $}

  ${
    $d y ph $.  $d z ph $.  $d z ps $.
    $( Distribution of existential quantifiers. $)
    3exdistr $p |- ( E. x E. y E. z ( ph /\ ps /\ ch ) <->
                E. x ( ph /\ E. y ( ps /\ E. z ch ) ) ) $=
      ( w3a wex wa 3anass biex 19.42v bilan 3bitr bitr ) ABCGZFHZEHZABCFHIZEHIZ
      DRASIZEHTQUAEQABCIZIZFHAUBFHZIUAPUCFABCJKAUBFLUDSABCFLMNKASELOK $.
      $( [9-Mar-95] $)
  $}

  ${
    $d y ph $.  $d z ph $.  $d w ph $.  $d z ps $.  $d w ps $.  $d w ch $.
    $( Distribution of existential quantifiers. $)
    4exdistr $p |- ( E. x E. y E. z E. w ( ( ph /\ ps ) /\ ( ch /\ th ) ) <->
                E. x ( ph /\ E. y ( ps /\ E. z ( ch /\ E. w th ) ) ) ) $=
      ( wa wex anass biex 19.42v bilan 3bitr bitr ) ABICDIZIZHJZGJZFJZABCDHJIZG
      JIZFJIZEUAAUCIZFJUDTUEFTABUBIZIZGJAUFGJZIUESUGGSABQIZIZHJZUGRUJHABQKLUKAU
      IHJZIABQHJZIZIUGAUIHMULUNABQHMNUNUFAUMUBBCDHMNNOPLAUFGMUHUCABUBGMNOLAUCFM
      PL $.
      $( [9-Mar-95] $)
  $}

  ${
    $d y ph $. $d x ps $.
    cbvalv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvalv $p |- ( A. x ph <-> A. y ps ) $=
      ( ax-17 cbval ) ABCDADFBCFEG $.
      $( [5-Aug-93] $)

    $( Rule used to change bound variables with implicit substitution. $)
    cbvexv $p |- ( E. x ph <-> E. y ps ) $=
      ( ax-17 cbvex ) ABCDADFBCFEG $.
      $( [5-Aug-93] $)
  $}

  ${
    $d y x $. $d y z $. $d w x $. $d w z $.
    cbval2.1 $e |- ( ph -> A. z ph ) $.
    cbval2.2 $e |- ( ph -> A. w ph ) $.
    cbval2.3 $e |- ( ps -> A. x ps ) $.
    cbval2.4 $e |- ( ps -> A. y ps ) $.
    cbval2.5 $e |- ( ( x = z /\ y = w ) -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbval2 $p |- ( A. x A. y ph <-> A. z A. w ps ) $=
      ( wal hbal weq wb wi wa ax-17 hban exp com12 pm5.32d cbval 19.28 3bitr3
      pm5.32 mpbir ) ADLZBFLZCEAEDGMBCFIMCENZUHUIOPUJUHQZUJUIQZOUJAQZDLUJBQZFLU
      KULUMUNDFUJAFUJFRZHSUJBDUJDRZJSDFNZUJABUJUQABOZUJUQURKTUAUBUCUJADUPUDUJBF
      UOUDUEUJUHUIUFUGUC $.
      $( [22-Dec-03] $) $( [22-Dec-03] $)

    $( Rule used to change bound variables with implicit substitution. $)
    cbvex2 $p |- ( E. x E. y ph <-> E. z E. w ps ) $=
      ( wex hbex weq wb wi wa ax-17 hban exp com12 pm5.32d cbvex 19.42 3bitr3
      pm5.32 mpbir ) ADLZBFLZCEAEDGMBCFIMCENZUHUIOPUJUHQZUJUIQZOUJAQZDLUJBQZFLU
      KULUMUNDFUJAFUJFRZHSUJBDUJDRZJSDFNZUJABUJUQABOZUJUQURKTUAUBUCUJADUPUDUJBF
      UOUDUEUJUHUIUFUGUC $.
      $( [14-Sep-03] $)
  $}

  ${
    $d z w ph $.  $d x y ps $.  $d x w $.  $d z y $.
    cbvex2v.1 $e |- ( ( x = z /\ y = w ) -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvex2v $p |- ( E. x E. y ph <-> E. z E. w ps ) $=
      ( ax-17 cbvex2 ) ABCDEFAEHAFHBCHBDHGI $.
      $( [26-Jul-95] $)
  $}

  ${
    $d x ph $.  $d x ch $.
    cbvald.1 $e |- ( ph -> A. y ph ) $.
    cbvald.2 $e |- ( ph -> ( ps -> A. y ps ) ) $.
    cbvald.3 $e |- ( ph -> ( x = y -> ( ps <-> ch ) ) ) $.
    $( Deduction used to change bound variables with implicit substitution,
       particularly useful in conjuction with ~ ddelim . $)
    cbvald $p |- ( ph -> ( A. x ps <-> A. y ch ) ) $=
      ( wal wi hbim1 ax-17 weq wb com12 pm5.74d cbval 19.21v 19.21 3bitr3
      pm5.74ri ) ABDIZCEIZABJZDIACJZEIAUBJAUCJUDUEDEABEFGKUEDLDEMZABCAUFBCNHOPQ
      ABDRACEFSTUA $.
      $( [2-Jan-02] $)

    $( Deduction used to change bound variables with implicit substitution,
       particularly useful in conjuction with ~ ddelim . $)
    cbvexd $p |- ( ph -> ( E. x ps <-> E. y ch ) ) $=
      ( wn wal wex hbnd weq wb pm4.11 syl6ib cbvald binegd df-ex 3bitr4g ) ABIZ
      DJZICIZEJZIBDKCEKAUBUDAUAUCDEFABEFGLADEMBCNUAUCNHBCOPQRBDSCEST $.
      $( [2-Jan-02] $)
  $}

  ${
    $v u $.
    $v f $.
    $v g $.
    $( Define temporary individual variables. $)
    cbvex4v.vu $f set u $.
    cbvex4v.vf $f set f $.
    cbvex4v.vg $f set g $.
    $d v u ph $.  $d f g ph $.  $d x y ch $.  $d z w ch $.
    $d x y ps $.  $d f g ps $.
    $d x z w u $.  $d y z w v $.  $d z g $.  $d w f $.
    cbvex4v.1 $e |- ( ( x = v /\ y = u ) -> ( ph <-> ps ) ) $.
    cbvex4v.2 $e |- ( ( z = f /\ w = g ) -> ( ps <-> ch ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvex4v $p |- ( E. x E. y E. z E. w ph <-> E. v E. u E. f E. g ch ) $=
      ( wex weq wa bi2exdv cbvex2v bi2ex bitr ) AGNFNZENDNBGNFNZINHNCKNJNZINHNU
      AUBDEHIDHOEIOPABFGLQRUBUCHIBCFGJKMRST $.
      $( [26-Jul-95] $)
  $}

  ${
    $d y ph $.  $d x ps $.
    $( Rearrange existential quantifiers. $)
    eeanv $p |- ( E. x E. y ( ph /\ ps ) <-> ( E. x ph /\ E. y ps ) ) $=
      ( wa wex exdistr ax-17 hbex 19.41 bitr ) ABEDFCFABDFZECFACFLEABCDGALCBCDB
      CHIJK $.
      $( [26-Jul-95] $)
  $}

  ${
    $d y z ph $.  $d x z ps $.  $d x y ch $.
    $( Rearrange existential quantifiers. $)
    eeeanv $p |- ( E. x E. y E. z ( ph /\ ps /\ ch ) <->
                 ( E. x ph /\ E. y ps /\ E. z ch ) ) $=
      ( wa wex w3a 19.42vv eeanv bilan bitr biex 19.41v 3anass bi3ex 3bitr4 )
      ABCGZGZFHEHZDHZADHZBEHZCFHZGZGZABCIZFHEHDHUCUDUEIUBAUFGZDHUGUAUIDUAASFHEH
      ZGUIASEFJUJUFABCEFKLMNAUFDOMUHTDEFABCPQUCUDUEPR $.
      $( [26-Jul-95] $)
  $}

  ${
    $d z ph $.  $d w ph $.  $d x ps $.  $d y ps $.  $d y z $.  $d w x $.
    $( Rearrange existential quantifiers. $)
    ee4anv $p |- ( E. x E. y E. z E. w ( ph /\ ps ) <->
                  ( E. x E. y ph /\ E. z E. w ps ) ) $=
      ( wa wex excom biex eeanv bi2ex 3bitr ) ABGFHZEHDHZCHNDHZEHZCHADHZBFHZGZE
      HCHRCHSEHGOQCNDEIJPTCEABDFKLRSCEKM $.
      $( [31-Jul-95] $)
  $}

  ${
    $d x ph $.
    nexdv.1 $e |- ( ph -> -. ps ) $.
    $( Deduction for generalization rule for negated wff. $)
    nexdv $p |- ( ph -> -. E. x ps ) $=
      ( ax-17 nexd ) ABCACEDF $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x ps $.
    chv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    chv.2 $e |- ph $.
    $( Implicit substitution of ` y ` for ` x ` into a theorem. $)
    chv $p |- ps $=
      ( a4b1 mpg ) ABCABCDEGFH $.
      $( [20-Apr-94] $)
  $}

  ${
    $d x z $.  $d y z $.
    $( When the class variables in definition ~ df-clel are replaced with set
       variables, this theorem of predicate calculus is the result.  This
       theorem provides part of the justification for the consistency of that
       definition, which "overloads" the set variables in ~ wel with the class
       variables in ~ wcel . $)
    cleljust $p |- ( x e. y <-> E. z ( z = x /\ z e. y ) ) $=
      ( weq wel wa wex ax-17 a13b eqsex bicom ) CADCBEZFCGABEZLMCAMCHCABIJK $.
      $( [28-Jan-04] $) $( [28-Jan-04] $)
  $}


