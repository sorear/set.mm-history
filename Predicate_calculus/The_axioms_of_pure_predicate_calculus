$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The axioms of pure predicate calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbols needed for pure predicate calculus. $)
  $c A. $. $( 'inverted A' universal quantifier (read:  'for all') $)
  $c var $. $( Individual variable type (read:  'the following is an
             individual variable' $)

  $( Declare some names for individual variables. $)
  $v x $.
  $v y $.
  $v z $.
  $v w $.
  $v v $.
  $( Let ` x ` be an individual variable. $)
  vx $f var x $.
  $( Let ` y ` be an individual variable. $)
  vy $f var y $.
  $( Let ` z ` be an individual variable. $)
  vz $f var z $.
  $( Let ` w ` be an individual variable. $)
  vw $f var w $.
  $( Let ` v ` be an individual variable. $)
  vv $f var v $.

  $( Extend wff definition to include the universal quantifier ('for all'). $)
  wal $a wff A. x ph $.

$(
   Postulate the four axioms of pure predicate calculus.  Note that these are
   valid even when ` x ` and ` y ` occur ('free' or not) in wff's ` ph `
   and ` ps `.  Thus we do not have to worry about 'free' variable
   restrictions that complicate the traditional textbook axioms.
$)

  $( Axiom of Specialization.  One of the 4 axioms of pure predicate calculus.
     Axiom scheme C5' of Megill, p. 448. $)
  ax-4 $a |- ( A. x ph -> ph ) $.

  $( Axiom of Quantified Implication.  One of the 4 axioms of pure predicate
     calculus.  Axiom scheme C4' of Megill, p. 448. $)
  ax-5 $a |- ( A. x ( A. x ph -> ps ) -> ( A. x ph -> A. x ps ) ) $.

  $( Axiom of Quantified Negation.  One of the 4 axioms of pure predicate
     calculus.  Compare axiom scheme C7' of Megill, p. 448. $)
  ax-6 $a |- ( -. A. x -. A. x ph -> ph ) $.

  $( Axiom of Quantifier Commutation.  One of the 4 axioms of pure predicate
     calculus.  Axiom scheme C6' of Megill, p. 448. $)
  ax-7 $a |- ( A. x A. y ph -> A. y A. x ph ) $.

  ${
    ax-g.1 $e |- ph $.
    $( Rule of Generalization.
       The postulated inference rule of pure predicate calculus. $)
    ax-gen   $a |- A. x ph $.
  $}


  $( Declare the existential quantifier symbol. $)
  $c E. $.    $( Backwards E (read:  'there exists') $)

  $( Extend wff definition to include the existential quantifier ('there
     exists'). $)
  wex $a wff E. x ph $.

  $( Define existential quantification. $)
  df-ex $a |- ( E. x ph <-> -. A. x -. ph ) $.

  ${
    a4i.1 $e |- A. x ph $.
    $( Inference rule reversing generalization. $)
    a4i   $p |- ph $=
      ( wal ax-4 ax-mp ) ABDAABECF $.
  $}

  ${
    a4s.1 $e |- ( ph -> ps ) $.
    $( Generalization of antecedent. $)
    a4s   $p |- ( A. x ph -> ps ) $=
      ( wal ax-4 syl ) ACEABACFDG $.
  $}

  ${
    a4sd.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction generalizing antecedent. $)
    a4sd   $p |- ( ph -> ( A. x ps -> ch ) ) $=
      ( wal ax-4 syl5 ) ABCBDFEBDGH $.
      $([17-Aug-94]$)
  $}

  ${
    mpg.1 $e |- ( A. x ph -> ps ) $.
    mpg.2 $e |- ph $.
    $( Modus ponens combined with generalization. $)
    mpg   $p |- ps $=
      ( wal ax-gen ax-mp ) ACFBDACEGH $.
      $([24-May-94]$)
  $}

  ${
    mpgbi.1 $e |- ( A. x ph <-> ps ) $.
    mpgbi.2 $e |- ph $.
    $( Modus ponens on biconditional combined with generalization. $)
    mpgbi   $p |- ps $=
      ( wal biimp mpg ) ABCACFBDGEH $.
      $([24-May-94]$)
  $}

  ${
    mpgbir.1 $e |- ( ph <-> A. x ps ) $.
    mpgbir.2 $e |- ps $.
    $( Modus ponens on biconditional combined with generalization. $)
    mpgbir   $p |- ph $=
      ( wal biimpr mpg ) BACABCFDGEH $.
      $([24-May-94]$)
  $}

  ${
    a5i.1 $e |- ( A. x ph -> ps ) $.
    $( Inference from ~ax-5 . $)
    a5i   $p |- ( A. x ph -> A. x ps ) $=
      ( wal wi ax-5 mpg ) ACEZBFIBCEFCABCGDH $.
  $}

  $( Abbreviated version of ~ax-6 . $)
  a6e $p |- ( E. x A. x ph -> ph ) $=
      ( wal wex wn df-ex ax-6 sylbi ) ABCZBDIEBCEAIBFABGH $.

  ${
    a7s.1 $e |- ( A. x A. y ph -> ps ) $.
    $( Swap quantifiers in an antecedent. $)
    a7s   $p |- ( A. y A. x ph -> ps ) $=
      ( wal ax-7 syl ) ACFDFADFCFBADCGEH $.
  $}

  $( Theorem 19.20, p. 90, of Margaris. $)
  19.20 $p |- ( A. x ( ph -> ps ) -> ( A. x ph -> A. x ps ) ) $=
      ( wi wal ax-4 syl4 a4s a5i ax-5 syl ) ABDZCEACEZBDZCEMBCEDLNCLNCMABACFGHI
      ABCJK $.

  ${
    19.20i.1 $e |- ( ph -> ps ) $.
    $( Inference quantifying both antecedent and conjunct. $)
    19.20i   $p |- ( A. x ph -> A. x ps ) $=
      ( a4s a5i ) ABCABCDEF $.
  $}

  ${
    19.20ii.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Inference quantifying antecedent, nested antecedent, and conjunct. $)
    19.20ii   $p |- ( A. x ph -> ( A. x ps -> A. x ch ) ) $=
      ( wal wi 19.20i 19.20 syl ) ADFBCGZDFBDFCDFGAKDEHBCDIJ $.
  $}

  $( Theorem 19.15, p. 90, of Margaris. $)
  19.15 $p |- ( A. x ( ph <-> ps ) -> ( A. x ph <-> A. x ps ) ) $=
      ( wb wal df-bi1 19.20ii df-bi2 impbid ) ABDZCEACEBCEJABCABFGJBACABHGI $.

  ${
    bial.1 $e |- ( ph <-> ps ) $.
    $( Inference adding universal quantifier to both sides of an
       equivalence. $)
    bial   $p |- ( A. x ph <-> A. x ps ) $=
      ( wb wal 19.15 mpg ) ABEACFBCFECABCGDH $.
      $([7-Aug-94]$)

    $( Inference adding 2 universal quantifiers to both sides of an
       equivalence. $)
    bi2al $p |- ( A. x A. y ph <-> A. x A. y ps ) $=
      ( wal bial ) ADFBDFCABDEGG $.
      $([9-Mar-97]$)
  $}


  $( The following "hypothesis-building" lemmas allow us to construct proofs of
     formulas of the form ` |- ( ph -> A. x ph ) ` from smaller formulas of
     this form.  These are useful for constructing hypotheses that state "` x `
     is (effectively) not free in ` ph `". $)

  ${
    hbth.1 $e |- ph $.
    $( No variable is (effectively) free in a theorem. $)
    hbth   $p |- ( ph -> A. x ph ) $=
      ( wal ax-gen a1i ) ABDAABCEF $.
  $}

  $( ` x ` is not free in ` A. x ph `.  Appendix example of Megill, p. 450. $)
  hba1 $p |- ( A. x ph -> A. x A. x ph ) $=
      ( wal id a5i ) AABCZBFDE $.

  ${
    hb.1 $e |- ( ph -> A. x ph ) $.
    $( If ` x ` is not free in ` ph `, it is not free in ` -. ph `. $)
    hbne $p |- ( -. ph -> A. x -. ph ) $=
      ( wal wn con3 19.20i ax-6 nsyl4 ) ABDZEZBDAEZBDAKLBAJCFGABHI $.

    $( If ` x ` is not free in ` ph `, it is not free in ` A. y ph `. $)
    hbal $p |- ( A. y ph -> A. x A. y ph ) $=
      ( wal 19.20i ax-7 syl ) ACEZABEZCEIBEAJCDFACBGH $.

    $( If ` x ` is not free in ` ph `, it is not free in ` E. y ph `. $)
    hbex $p |- ( E. y ph -> A. x E. y ph ) $=
      ( wn wal wex hbne hbal df-ex bial 3imtr4 ) AEZCFZEZOBFACGZPBFNBMBCABDHIHA
      CJZPOBQKL $.
    ${

      hb.2 $e |- ( ps -> A. x ps ) $.
      $( If ` x ` is not free in ` ph ` and ` ps `, it is not free in
         ` ( ph -> ps ) `. $)
      hbim $p |- ( ( ph -> ps ) -> A. x ( ph -> ps ) ) $=
      ( wi wal wn hbne pm2.21 19.20i syl ax-1 ja ) ABABFZCGZAHZQCGPACDIQOCABJKL
      BBCGPEBOCBAMKLN $.

      $( If ` x ` is not free in ` ph ` and ` ps `, it is not free in
         ` ( ph \/ ps ) `. $)
      hbor $p |- ( ( ph \/ ps ) -> A. x ( ph \/ ps ) ) $=
      ( wn wi wal wo hbne hbim df-or bial 3imtr4 ) AFZBGZPCHABIZQCHOBCACDJEKABL
      ZQPCRMN $.

      $( If ` x ` is not free in ` ph ` and ` ps `, it is not free in
         ` ( ph /\ ps ) `. $)
      hban $p |- ( ( ph /\ ps ) -> A. x ( ph /\ ps ) ) $=
      ( wn wi wal wa hbne hbim df-an bial 3imtr4 ) ABFZGZFZQCHABIZRCHPCAOCDBCEJ
      KJABLZRQCSMN $.

      $( If ` x ` is not free in ` ph ` and ` ps `, it is not free in
         ` ( ph <-> ps ) `. $)
      hbbi $p |- ( ( ph <-> ps ) -> A. x ( ph <-> ps ) ) $=
      ( wi wa wal wb hbim hban bi bial 3imtr4 ) ABFZBAFZGZQCHABIZRCHOPCABCDEJBA
      CEDJKABLZRQCSMN $.
    $}
  $}

  $( ` x ` is not free in ` -. A. x ph `. $)
  hbn1 $p |- ( -. A. x ph -> A. x -. A. x ph ) $=
      ( wal hba1 hbne ) ABCBABDE $.

  $( ` x ` is not free in ` E. x ph `. $)
  hbe1 $p |- ( E. x ph -> A. x E. x ph ) $=
      ( wn wal wex hbn1 df-ex bial 3imtr4 ) ACZBDCZKBDABEZLBDJBFABGZLKBMHI $.

  $( A closed form of hypothesis builder ~hbne . $)
  hbnt $p |- ( A. x ( ph -> A. x ph ) -> ( -. ph -> A. x -. ph ) ) $=
      ( wal wi wn pm2.16 19.20ii ax-6 con1 syl5 ) AABCZDZBCKEZBCZAEZBCOLMOBAKFG
      NAABHIJ $.

  $( If a wff is true, it is true for at least one instance. $)
  19.8a $p |- ( ph -> E. x ph ) $=
      ( wn wal wex ax-4 con2 df-ex sylibr ) AACZBDZCABEKAJBFGABHI $.

  $( Theorem 19.2, p. 89, of Margaris. $)
  19.2 $p |- ( A. x ph -> E. x ph ) $=
      ( wex 19.8a a4s ) AABCBABDE $.

  ${
    19.3r.1 $e |- ( ph -> A. x ph ) $.
    $( A wff may be quantified with a variable not free in it. $)
    19.3r   $p |- ( ph <-> A. x ph ) $=
      ( wal ax-4 impbi ) AABDCABEF $.
  $}

  $( Theorem 19.5, p. 89, of Margaris. $)
  19.5 $p |- ( A. x A. y ph <-> A. y A. x ph ) $=
      ( wal ax-7 impbi ) ACDBDABDCDABCEACBEF $.

  $( Theorem 19.7, p. 89, of Margaris. $)
  19.7 $p |- ( A. x -. ph <-> -. E. x ph ) $=
      ( wex wn wal df-ex bicon2 ) ABCADBEABFG $.

  $( Theorem 19.6, p. 89, of Margaris. $)
  19.6 $p |- ( A. x ph <-> -. E. x -. ph ) $=
      ( wal wn wex pm4.13 bial 19.7 bitr ) ABCADZDZBCJBEDAKBAFGJBHI $.

  ${
    19.9r.1 $e |- ( ph -> A. x ph ) $.
    $( Variation of Theorem 19.9, p. 89, of Margaris. $)
    19.9r   $p |- ( ph <-> E. x ph ) $=
      ( wex 19.8a wn wal df-ex con3 19.20i ax-6 syl sylbi impbi ) AABDZABEOAFZB
      GZFZAABHRABGZFZBGZFAUAQTPBASCIJIABKLMN $.
  $}

  $( A closed version of one direction of ~19.9r . $)
  19.9t $p |- ( A. x ( ph -> A. x ph ) -> ( E. x ph -> ph ) ) $=
      ( wal wi wn wex hbnt pm2.15d df-ex syl5ib ) AABCDBCZAEBCZEAABFKALABGHABI
      J $.

  ${
    19.9d.1 $e |- ( ps -> A. x ps ) $.
    19.9d.2 $e |- ( ps -> ( ph -> A. x ph ) ) $.
    $( A deduction version of one direction of ~19.9r . $)
    19.9d   $p |- ( ps -> ( E. x ph -> ph ) ) $=
      ( wal wi wex 19.20i 19.9t 3syl ) BBCFAACFGZCFACHAGDBLCEIACJK $.
  $}

  $( Theorem 19.14, p. 90, of Margaris. $)
  19.14 $p |- ( E. x -. ph <-> -. A. x ph ) $=
      ( wal wn wex 19.6 bicon2 ) ABCADBEABFG $.

  $( Theorem 19.22, p. 90, of Margaris. $)
  19.22 $p |- ( A. x ( ph -> ps ) -> ( E. x ph -> E. x ps ) ) $=
      ( wi wal wn wex pm2.16 19.20ii pm2.16d df-ex 3imtr4g ) ABDZCEZAFZCEZFBFZ
      CEZFACGBCGNRPMQOCABHIJACKBCKL $.

  ${
    19.22i.1 $e |- ( ph -> ps ) $.
    $( Inference adding existential quantifier to antecedent and conjunct. $)
    19.22i   $p |- ( E. x ph -> E. x ps ) $=
      ( wi wex 19.22 mpg ) ABEACFBCFECABCGDH $.
  $}

  $( A transformation of quantifiers and logical connectives. $)
  alnex $p |- ( A. x ( ph -> -. ps ) <-> -. E. x ( ph /\ ps ) ) $=
      ( wn wi wal wa wex imnan bial 19.7 bitr ) ABDEZCFABGZDZCFNCHDMOCABIJNCKL
      $.
      $([19-Aug-93]$)

  $( A transformation of quantifiers and logical connectives. $)
  exnal $p |- ( E. x ( ph /\ -. ps ) <-> -. A. x ( ph -> ps ) ) $=
      ( wi wal wn wa wex iman bial 19.7 bitr bicon2 ) ABDZCEZABFGZCHZOPFZCEQFNR
      CABIJPCKLM $.
      $([25-Mar-96]$)

  $( A relationship between two quantifiers and negation. $)
  alexn $p |- ( A. x E. y -. ph <-> -. E. x A. y ph ) $=
      ( wn wex wal 19.14 bial 19.7 bitr ) ADCEZBFACFZDZBFLBEDKMBACGHLBIJ $.
      $([18-Aug-93]$)

  $( One direction of Theorem 19.11, p. 89, of Margaris. $)
  19.11a $p |- ( E. x E. y ph -> E. y E. x ph ) $=
      ( wex 19.8a 19.22i hbe1 hbex 19.9r sylibr ) ACDZBDABDZCDZBDMKMBALCABEFFMB
      LBCABGHIJ $.

  $( Theorem 19.11, p. 89, of Margaris. $)
  19.11 $p |- ( E. x E. y ph <-> E. y E. x ph ) $=
      ( wex 19.11a impbi ) ACDBDABDCDABCEACBEF $.

  $( Theorem 19.12, p. 89, of Margaris. $)
  19.12 $p |- ( E. x A. y ph -> A. y E. x ph ) $=
      ( wal wex hba1 hbex ax-4 19.22i 19.20i syl ) ACDZBEZMCDABEZCDLCBACFGMNCLA
      BACHIJK $.

  $( Theorem 19.13, p. 89, of Margaris. $)
  19.13 $p |- ( E. x ph <-> -. A. x -. ph ) $=
      ( df-ex ) ABC $.

  ${
    19.16.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.16, p. 90, of Margaris. $)
    19.16   $p |- ( A. x ( ph <-> ps ) -> ( ph <-> A. x ps ) ) $=
      ( wb wal 19.15 19.3r syl5bb ) ABECFACFBCFAABCGACDHI $.
  $}

  ${
    19.17.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.17, p. 90, of Margaris. $)
    19.17   $p |- ( A. x ( ph <-> ps ) -> ( A. x ph <-> ps ) ) $=
      ( wb wal 19.15 19.3r syl6bbr ) ABECFACFBCFBABCGBCDHI $.
  $}

  $( Theorem 19.18, p. 90, of Margaris. $)
  19.18 $p |- ( A. x ( ph <-> ps ) -> ( E. x ph <-> E. x ps ) ) $=
      ( wb wal wex wi df-bi1 19.20i 19.22 syl df-bi2 impbid ) ABDZCEZACFZBCFZO
      ABGZCEPQGNRCABHIABCJKOBAGZCEQPGNSCABLIBACJKM $.

  ${
    biex.1 $e |- ( ph <-> ps ) $.
    $( Inference adding existential quantifier to both sides of an
       equivalence. $)
    biex   $p |- ( E. x ph <-> E. x ps ) $=
      ( wb wex 19.18 mpg ) ABEACFBCFECABCGDH $.
      $([24-May-94]$)
  $}

  ${
    bi2ex.1 $e |- ( ph <-> ps ) $.
    $( Inference adding 2 existential quantifiers to both sides of an
       equivalence. $)
    bi2ex   $p |- ( E. x E. y ph <-> E. x E. y ps ) $=
      ( wex biex ) ADFBDFCABDEGG $.
      $([16-Mar-95]$)
  $}

  ${
    bi3ex.1 $e |- ( ph <-> ps ) $.
    $( Inference adding 3 existential quantifiers to both sides of an
       equivalence. $)
    bi3ex   $p |- ( E. x E. y E. z ph <-> E. x E. y E. z ps ) $=
      ( wex biex bi2ex ) AEGBEGCDABEFHI $.
      $([2-May-95]$)
  $}

  $( Commutation of conjunction inside an existential quantifier. $)
  exancom $p |- ( E. x ( ph /\ ps ) <-> E. x ( ps /\ ph ) ) $=
      ( wa ancom biex ) ABDBADCABEF $.
      $([18-Aug-93]$)

  ${
    19.19.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.19, p. 90, of Margaris. $)
    19.19   $p |- ( A. x ( ph <-> ps ) -> ( ph <-> E. x ps ) ) $=
      ( wb wal wex 19.18 19.9r syl5bb ) ABECFACGBCGAABCHACDIJ $.
  $}

  ${
    19.21.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.21, p. 90, of Margaris. $)
    19.21   $p |- ( A. x ( ph -> ps ) <-> ( ph -> A. x ps ) ) $=
      ( wi wal 19.20 syl5 hba1 hbim ax-4 syl3 19.20i syl impbi ) ABEZCFZABCFZEZ
      QACFRAABCGDHSSCFQARCDBCIJSPCRBABCKLMNO $.
  $}

  ${
    stdpc5.1 $e |- ( ph -> A. x ph ) $.
    $( An axiom of standard predicate calculus.   Axiom 5 of Mendelson,
       p. 59. $)
    stdpc5   $p |- ( A. x ( ph -> ps ) -> ( ph -> A. x ps ) ) $=
      ( wi wal 19.21 biimp ) ABECFABCFEABCDGH $.
      $([22-Sep-93]$)
  $}

  ${
    19.21ai.1 $e |- ( ph -> A. x ph ) $.
    19.21ai.2 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.21, p. 90, of Margaris. $)
    19.21ai   $p |- ( ph -> A. x ps ) $=
      ( wal 19.20i syl ) AACFBCFDABCEGH $.
  $}

  ${
    19.21ad.1 $e |- ( ph -> A. x ph ) $.
    19.21ad.2 $e |- ( ps -> A. x ps ) $.
    19.21ad.3 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.21, p. 90, of Margaris. $)
    19.21ad   $p |- ( ph -> ( ps -> A. x ch ) ) $=
      ( wal wa hban imp 19.21ai exp ) ABCDHABICDABDEFJABCGKLM $.
      $([10-Feb-97]$)
  $}

  ${
    19.21bi.1 $e |- ( ph -> A. x ps ) $.
    $( Inference from Theorem 19.21, p. 90, of Margaris. $)
    19.21bi   $p |- ( ph -> ps ) $=
      ( wal ax-4 syl ) ABCEBDBCFG $.
  $}

  ${
    19.21bbi.1 $e |- ( ph -> A. x A. y ps ) $.
    $( Inference removing double quantifier. $)
    19.21bbi   $p |- ( ph -> ps ) $=
      ( wal 19.21bi ) ABDABDFCEGG $.
      $([20-Apr-94]$)
  $}

  ${
    19.22d.1 $e |- ( ph -> A. x ph ) $.
    19.22d.2 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.22, p. 90, of Margaris. $)
    19.22d   $p |- ( ph -> ( E. x ps -> E. x ch ) ) $=
      ( wi wal wex 19.21ai 19.22 syl ) ABCGZDHBDICDIGAMDEFJBCDKL $.
      $([20-May-96]$)
  $}

  ${
    19.23.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.23, p. 90, of Margaris. $)
    19.23   $p |- ( A. x ( ph -> ps ) <-> ( E. x ph -> ps ) ) $=
      ( wi wal wex 19.22 19.9r syl6ibr hbe1 hbim 19.8a syl4 19.21ai impbi ) ABE
      ZCFZACGZBEZRSBCGBABCHBCDIJTQCSBCACKDLASBACMNOP $.
  $}

  ${
    19.23ai.1 $e |- ( ps -> A. x ps ) $.
    19.23ai.2 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.23, p. 90, of Margaris. $)
    19.23ai   $p |- ( E. x ph -> ps ) $=
      ( wex 19.22i 19.9r sylibr ) ACFBCFBABCEGBCDHI $.
  $}

  ${
    19.23bi.1 $e |- ( E. x ph -> ps ) $.
    $( Inference from Theorem 19.23, p. 90, of Margaris. $)
    19.23bi   $p |- ( ph -> ps ) $=
      ( wex 19.8a syl ) AACEBACFDG $.
  $}

  ${
    19.23ad.1 $e |- ( ph -> A. x ph ) $.
    19.23ad.2 $e |- ( ch -> A. x ch ) $.
    19.23ad.3 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.23, p. 90, of Margaris. $)
    19.23ad   $p |- ( ph -> ( E. x ps -> ch ) ) $=
      ( wi wal wex 19.21ai 19.23 sylib ) ABCHZDIBDJCHANDEGKBCDFLM $.
      $([28-Jan-97]$)
  $}

  $( Theorem 19.26, p. 90, of Margaris. $)
  19.26 $p |- ( A. x ( ph /\ ps ) <-> ( A. x ph /\ A. x ps ) ) $=
      ( wa wal pm3.26 19.20i pm3.27 jca pm3.2 19.20ii imp impbi ) ABDZCEZACEZBC
      EZDOPQNACABFGNBCABHGIPQOABNCABJKLM $.

  ${
    19.27.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.27, p. 90, of Margaris. $)
    19.27   $p |- ( A. x ( ph /\ ps ) <-> ( A. x ph /\ ps ) ) $=
      ( wa wal 19.26 19.3r bilan bitr4 ) ABECFACFZBCFZEKBEABCGBLKBCDHIJ $.
  $}

  ${
    19.28.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.28, p. 90, of Margaris. $)
    19.28   $p |- ( A. x ( ph /\ ps ) <-> ( ph /\ A. x ps ) ) $=
      ( wa wal 19.26 19.3r biran bitr4 ) ABECFACFZBCFZEALEABCGAKLACDHIJ $.
  $}

  $( Theorem 19.29, p. 90, of Margaris. $)
  19.29 $p |- ( ( A. x ph /\ E. x ps ) -> E. x ( ph /\ ps ) ) $=
      ( wal wex wa wn wi 19.20 19.7 syl6ib con3 df-an 19.14 3imtr4 biex sylibr
      ) ACDZBCEZFZABGZHZGZCEZABFZCERSGZHZGUBCDZGTUDUHUGUHRUACDUFAUACIBCJKLRSMUB
      CNOUEUCCABMPQ $.

  $( Variation of Theorem 19.29, p. 90, of Margaris. $)
  19.29r $p |- ( ( E. x ph /\ A. x ps ) -> E. x ( ph /\ ps ) ) $=
      ( wal wex wa 19.29 ancom exancom 3imtr4 ) BCDZACEZFBAFCELKFABFCEBACGLKHAB
      CIJ $.
      $([18-Aug-93]$)

  $( Theorem 19.35, p. 90, of Margaris. $)
  19.35 $p |- ( E. x ( ph -> ps ) <-> ( A. x ph -> E. x ps ) ) $=
      ( wal wn wi wex wa iman bicom bicon1 bial 19.26 df-an 3bitr bicon2 df-ex
      bilim 3bitr4r ) ACDZBEZCDZEZFZABFZEZCDZETBCGZFUECGUGUDUGAUAHZCDTUBHUDEUFU
      ICUIUEUEUIEABIJKLAUACMTUBNOPUHUCTBCQRUECQS $.

  ${
    19.35i.1 $e |- E. x ( ph -> ps ) $.
    $( Inference from Theorem 19.35, p. 90, of Margaris. $)
    19.35i   $p |- ( A. x ph -> E. x ps ) $=
      ( wi wex wal 19.35 mpbi ) ABECFACGBCFEABCHDI $.
  $}

  ${
    19.35ri.1 $e |- ( A. x ph -> E. x ps ) $.
    $( Inference from Theorem 19.35, p. 90, of Margaris. $)
    19.35ri   $p |- E. x ( ph -> ps ) $=
      ( wi wex wal 19.35 mpbir ) ABECFACGBCFEABCHDI $.
  $}

  ${
    19.36.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.36, p. 90, of Margaris. $)
    19.36   $p |- ( E. x ( ph -> ps ) <-> ( A. x ph -> ps ) ) $=
      ( wi wex wal 19.35 19.9r bilim bitr4 ) ABECFACGZBCFZELBEABCHBMLBCDIJK $.
  $}

  ${
    19.36i.1 $e |- ( ps -> A. x ps ) $.
    19.36i.2 $e |- E. x ( ph -> ps ) $.
    $( Inference from Theorem 19.36, p. 90, of Margaris. $)
    19.36i   $p |- ( A. x ph -> ps ) $=
      ( wi wex wal 19.36 mpbi ) ABFCGACHBFABCDIEJ $.
  $}

  ${
    19.37.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.37, p. 90, of Margaris. $)
    19.37   $p |- ( E. x ( ph -> ps ) <-> ( ph -> E. x ps ) ) $=
      ( wi wex wal 19.35 19.3r birim bitr4 ) ABECFACGZBCFZEAMEABCHALMACDIJK $.
  $}

  $( Theorem 19.38, p. 90, of Margaris. $)
  19.38 $p |- ( ( E. x ph -> A. x ps ) -> A. x ( ph -> ps ) ) $=
      ( wex wal wi hbe1 hba1 hbim 19.8a ax-4 syl34 19.21ai ) ACDZBCEZFABFCNOCAC
      GBCHIANOBACJBCKLM $.

  $( Theorem 19.39, p. 90, of Margaris. $)
  19.39 $p |- ( ( E. x ph -> E. x ps ) -> E. x ( ph -> ps ) ) $=
      ( wex wi wal 19.2 syl4 19.35 sylibr ) ACDZBCDZEACFZLEABECDMKLACGHABCIJ $.

  $( Theorem 19.24, p. 90, of Margaris. $)
  19.24 $p |- ( ( A. x ph -> A. x ps ) -> E. x ( ph -> ps ) ) $=
      ( wal wi wex 19.2 syl3 19.35 sylibr ) ACDZBCDZEKBCFZEABECFLMKBCGHABCIJ $.

  $( Theorem 19.25, p. 90, of Margaris. $)
  19.25 $p |- ( A. y E. x ( ph -> ps ) ->
              ( E. y A. x ph -> E. y E. x ps ) ) $=
      ( wi wex wal 19.35 biimp 19.20i 19.22 syl ) ABECFZDGACGZBCFZEZDGNDFODFEMP
      DMPABCHIJNODKL $.

  $( Theorem 19.30, p. 90, of Margaris. $)
  19.30 $p |- ( A. x ( ph \/ ps ) -> ( A. x ph \/ E. x ps ) ) $=
      ( wn wi wal wo wex 19.20 orcom df-or bitr bial df-ex bilor imor 3bitr4
      3imtr4 ) BDZAEZCFSCFZACFZEZABGZCFUBBCHZGZSACIUDTCUDBAGTABJBAKLMUBUADZGUGU
      BGUFUCUBUGJUEUGUBBCNOUAUBPQR $.

  ${
    19.32.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.32, p. 90, of Margaris. $)
    19.32   $p |- ( A. x ( ph \/ ps ) <-> ( ph \/ A. x ps ) ) $=
      ( wn wi wal wo hbne 19.21 df-or bial 3bitr4 ) AEZBFZCGNBCGZFABHZCGAPHNBCA
      CDIJQOCABKLAPKM $.
  $}

  ${
    19.31.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.31, p. 90, of Margaris. $)
    19.31   $p |- ( A. x ( ph \/ ps ) <-> ( A. x ph \/ ps ) ) $=
      ( wo wal 19.32 orcom bial 3bitr4 ) BAEZCFBACFZEABEZCFLBEBACDGMKCABHILBHJ
      $.
  $}

  $( Theorem 19.43, p. 90, of Margaris. $)
  19.43 $p |- ( E. x ( ph \/ ps ) <-> ( E. x ph \/ E. x ps ) ) $=
      ( wo wn wal wex wa ioran bial 19.26 19.7 bi2an 3bitr bineg df-ex oran
      3bitr4 ) ABDZEZCFZEACGZEZBCGZEZHZESCGUBUDDUAUFUAAEZBEZHZCFUGCFZUHCFZHUFTU
      ICABIJUGUHCKUJUCUKUEACLBCLMNOSCPUBUDQR $.

  ${
    19.44.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.44, p. 90, of Margaris. $)
    19.44   $p |- ( E. x ( ph \/ ps ) <-> ( E. x ph \/ ps ) ) $=
      ( wo wex 19.43 19.9r bilor bitr4 ) ABECFACFZBCFZEKBEABCGBLKBCDHIJ $.
  $}

  ${
    19.45.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.45, p. 90, of Margaris. $)
    19.45   $p |- ( E. x ( ph \/ ps ) <-> ( ph \/ E. x ps ) ) $=
      ( wo wex 19.43 19.9r biror bitr4 ) ABECFACFZBCFZEALEABCGAKLACDHIJ $.
  $}

  $( Theorem 19.33, p. 90, of Margaris. $)
  19.33 $p |- ( ( A. x ph \/ A. x ps ) -> A. x ( ph \/ ps ) ) $=
      ( wn wex wal wi wo 19.38 df-or 19.14 birim bitr4 bial 3imtr4 ) ADZCEZBCFZ
      GZPBGZCFACFZRHZABHZCFPBCIUBUADZRGSUARJQUDRACKLMUCTCABJNO $.

  $( Theorem 19.34, p. 90, of Margaris. $)
  19.34 $p |- ( ( A. x ph \/ E. x ps ) -> E. x ( ph \/ ps ) ) $=
      ( wal wex wo 19.2 imror 19.43 sylibr ) ACDZBCEZFACEZLFABFCEKMLACGHABCIJ
      $.

  $( Theorem 19.40, p. 90, of Margaris. $)
  19.40 $p |- ( E. x ( ph /\ ps ) -> ( E. x ph /\ E. x ps ) ) $=
      ( wa wex pm3.26 19.22i pm3.27 jca ) ABDZCEACEBCEJACABFGJBCABHGI $.

  ${
    19.41.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.41, p. 90, of Margaris. $)
    19.41   $p |- ( E. x ( ph /\ ps ) <-> ( E. x ph /\ ps ) ) $=
      ( wa wex wn wal df-ex wo hbne 19.31 ianor bial 19.7 biror bitr4 3bitr4
      bicon2 ) ABEZCFTGZCHZGACFZBEZTCIUBUDAGZBGZJZCHUECHZUFJZUBUDGZUEUFCBCDKLUA
      UGCABMNUJUCGZUFJUIUCBMUHUKUFACOPQRSQ $.
  $}

  ${
    19.42.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.42, p. 90, of Margaris. $)
    19.42   $p |- ( E. x ( ph /\ ps ) <-> ( ph /\ E. x ps ) ) $=
      ( wa wex 19.41 exancom ancom 3bitr4 ) BAECFBCFZAEABECFAKEBACDGABCHAKIJ $.
      $([18-Aug-93]$)
  $}

  $( Swap 1st and 3rd existential quantifiers. $)
  excom13 $p |- ( E. x E. y E. z ph <-> E. z E. y E. x ph ) $=
      ( wex 19.11 biex 3bitr ) ADEZCEBEIBEZCEABEZDEZCEKCEDEIBCFJLCABDFGKCDFH $.
      $([9-Mar-95]$)

  $( Rotate existential quantifiers. $)
  exrot3 $p |- ( E. x E. y E. z ph <-> E. y E. z E. x ph ) $=
      ( wex excom13 19.11 bitr ) ADECEBEABEZCEDEIDECEABCDFIDCGH $.
      $([17-Mar-95]$)

  $( Rotate existential quantifiers twice. $)
  exrot4 $p |- ( E. x E. y E. z E. w ph <-> E. z E. w E. x E. y ph ) $=
      ( wex excom13 biex bitr ) AEFDFCFZBFACFZDFEFZBFKBFEFDFJLBACDEGHKBEDGI $.
      $([9-Mar-95]$)

  ${
    nex.1 $e |- -. ph $.
    $( Generalization rule for negated wff. $)
    nex   $p |- -. E. x ph $=
      ( wn wex 19.7 mpgbi ) ADABEDBABFCG $.
      $([18-May-94]$)
  $}

  ${
    hbim1.1 $e |- ( ph -> A. x ph ) $.
    hbim1.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( A closed form of ~hbim . $)
    hbim1   $p |- ( ( ph -> ps ) -> A. x ( ph -> ps ) ) $=
      ( wi wal a2i 19.21 sylibr ) ABFZABCGZFKCGABLEHABCDIJ $.
  $}

  ${
    biald.1 $e |- ( ph -> A. x ph ) $.
    biald.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for universal quantifier (deduction rule). $)
    biald   $p |- ( ph -> ( A. x ps <-> A. x ch ) ) $=
      ( wb wal 19.21ai 19.15 syl ) ABCGZDHBDHCDHGALDEFIBCDJK $.
  $}

  ${
    biexd.1 $e |- ( ph -> A. x ph ) $.
    biexd.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for existential quantifier (deduction rule). $)
    biexd   $p |- ( ph -> ( E. x ps <-> E. x ch ) ) $=
      ( wb wal wex 19.21ai 19.18 syl ) ABCGZDHBDICDIGAMDEFJBCDKL $.
  $}

  ${
    exan.1 $e |- ( E. x ph /\ ps ) $.
    $( Place a conjunct in the scope of an existential quantifier. $)
    exan   $p |- E. x ( ph /\ ps ) $=
      ( wa wex exancom wal 19.29 hbe1 19.27 ancom mpbi mpgbi ax-mp ) BAECFZABEC
      FBACGBCHACFZEZPBACIBQEZRCBQCACJKQBESQBLDMNOM $.
      $([18-Aug-93]$)
  $}

  $( Split biconditional and distribute quantifier. $)
  albi $p |- ( A. x ( ph <-> ps ) <->
             ( A. x ( ph -> ps ) /\ A. x ( ps -> ph ) ) ) $=
      ( wb wal wi wa bi bial 19.26 bitr ) ABDZCEABFZBAFZGZCEMCENCEGLOCABHIMNCJK
      $.
      $([18-Aug-93]$)

  ${
    hbimd.1 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hbimd.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Deduction form of ~hbim . $)
    hbimd   $p |- ( A. x ph -> ( ( ps -> ch ) -> A. x ( ps -> ch ) ) ) $=
      ( wi wal wn 19.20i hbnt syl comm pm2.21 syl6 a4s ax-1 ja ) BCGZADHZSDHZBC
      TUAGBIZTUBDHZUATUBUCTBBDHGZDHUBUCGAUDDEJBDKLMUBSDBCNJOCTCDHZUATCUEACUEGDF
      PMCSDCBQJORM $.
  $}

  ${
    hbbid.1 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hbbid.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Deduction form of ~hbbi . $)
    hbbid   $p |- ( A. x ph -> ( ( ps <-> ch ) -> A. x ( ps <-> ch ) ) ) $=
      ( wal wi wa wb hbimd im2and bi albi 3imtr4g ) ADGZBCHZCBHZIQDGZRDGZIBCJ
      ZUADGPQSRTABCDEFKACBDFEKLBCMBCDNO $.
      $([8-Mar-95]$)
  $}

  $( Direct form of Theorem 19.21, p. 90, of Margaris. $)
  19.21g $p |- ( A. x ( ph -> A. x ph ) ->
               ( A. x ( ph -> ps ) <-> ( ph -> A. x ps ) ) ) $=
      ( wal wi 19.20 syl3d comm a4s id hba1 a1i hbimd ax-4 syl3 19.20i syl6
      impbid ) AACDZEZCDZABEZCDZABCDZEZTUCUEECUCTUEUCSUDAABCFGHIUAUEUECDUCTAUDC
      TJUDUDCDETBCKLMUEUBCUDBABCNOPQR $.
      $([27-May-97]$)

  $( Introduce a conjunct in the scope of an existential quantifier. $)
  exintr $p |- ( A. x ( ph -> ps ) -> ( E. x ph -> E. x ( ph /\ ps ) ) ) $=
      ( wi wal wn wa wex pm2.65 imnan syl5ibr 19.20ii pm2.16d df-ex 3imtr4g )
      ABDZCEZAFZCEZFABGZFZCEZFACHTCHQUBSPUARCPABFDRUAABIABJKLMACNTCNO $.
      $([11-Aug-93]$)

  ${
    aaan.1 $e |- ( ph -> A. y ph ) $.
    aaan.2 $e |- ( ps -> A. x ps ) $.
    $( Rearrange universal quantifiers. $)
    aaan   $p |- ( A. x A. y ( ph /\ ps ) <-> ( A. x ph /\ A. y ps ) ) $=
      ( wa wal 19.28 bial hbal 19.27 bitr ) ABGDHZCHABDHZGZCHACHOGNPCABDEIJAOCB
      CDFKLM $.
      $([12-Aug-93]$)
  $}

  ${
    eeor.1 $e |- ( ph -> A. y ph ) $.
    eeor.2 $e |- ( ps -> A. x ps ) $.
    $( Rearrange existential quantifiers. $)
    eeor   $p |- ( E. x E. y ( ph \/ ps ) <-> ( E. x ph \/ E. y ps ) ) $=
      ( wo wex 19.45 biex hbex 19.44 bitr ) ABGDHZCHABDHZGZCHACHOGNPCABDEIJAOCB
      CDFKLM $.
      $([8-Aug-94]$)
  $}


