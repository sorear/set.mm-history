$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The axioms of pure predicate calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbols needed for pure predicate calculus. $)
  $c A. $. $( 'inverted A' universal quantifier (read:  'for all') $)
  $c set $. $( Individual variable type (read:  'the following is an
             individual (set) variable' $)

  $( Declare some names for individual variables. $)
  $v x $.
  $v y $.
  $v z $.
  $v w $.
  $v v $.
  $( Let ` x ` be an individual variable. $)
  vx $f set x $.
  $( Let ` y ` be an individual variable. $)
  vy $f set y $.
  $( Let ` z ` be an individual variable. $)
  vz $f set z $.
  $( Let ` w ` be an individual variable. $)
  vw $f set w $.
  $( Let ` v ` be an individual variable. $)
  vv $f set v $.

  $( Extend wff definition to include the universal quantifier ('for all').
     ` A. x ph ` is read " ` ph ` (phi) is true fo all ` x ` ."  Typically,
     in its final application ` ph ` would be replaced with a wff containing
     a (free) occurrence of the variable ` x ` , for example ` x = y ` .
     In a universe with a finite number of objects, "for all" is equivalent
     to a big conjunction (AND) with one wff for each possible case of
     ` x ` .  When the universe is infinite (as with set theory), such a
     propositional-calculus equivalent is not possible because an infinitely
     long formula has no meaning, but conceptually the idea is the same. $)
  wal $a wff A. x ph $.

$(
   Postulate the four axioms of pure predicate calculus.  Note that these are
   valid even when ` x ` and ` y ` occur ('free' or not) in wff's ` ph `
   and ` ps ` .  Thus we do not have to worry about 'free' variable
   restrictions that complicate the traditional textbook axioms.
$)

  $( Axiom of Specialization.  A quantified wff implies the wff without a
     quantifier (i.e. an instance, or special case, of the generalized wff).
     In other words if something is true for all ` x ` , it is true for any
     specific ` x ` (that would typically occur as a free variable in the wff
     substituted for ` ph ` ). (A free variable is one that does not occur in
     the scope of a quantifier:  ` x ` and ` y ` are both free in
     ` x = y ` , but only ` y ` is free in ` A. x x = y ` .) This is one of
     the 4 axioms of what we call "pure" predicate calculus.  Unlike the more
     typical textbook Axiom of Specialization, we cannot choose a variable
     different from ` x ` for the special case.  That is dealt with later
     when substitution is introduced - see ~ stdpc4 . Axiom scheme C5' in
     [Megill] p. 448 (p. 16 of the preprint).  Also appears as Axiom B5 of
     [Tarski] p. 67 (under his system S2, defined in the last paragraph on
     p. 77).  Note that the converse of this axiom does not hold in general,
     but a weaker inference form of the converse holds and is expressed as
     rule ~ ax-gen . Conditional forms of the converse are given by ~ ax-12 ,
     ~ ax-15 , ~ ax-16 , and ~ ax-17 . $)
  ax-4 $a |- ( A. x ph -> ph ) $.

  $( Axiom of Quantified Implication.  This axiom moves a quantifier from
     outside to inside an implication, quantifying ` ps ` .  Notice that
     ` x ` must not be a free variable in the antecedent of the quantified
     implication, and we express this by binding ` ph ` to "protect" the
     axiom from a ` ph ` containing a free ` x ` .
     One of the 4 axioms of pure predicate calculus.  Axiom scheme C4' in
     [Megill] p. 448 (p. 16 of the preprint).  It is a special case of Lemma 5
     of [Monk2] p. 108. $)
  ax-5 $a |- ( A. x ( A. x ph -> ps ) -> ( A. x ph -> A. x ps ) ) $.

  $( Axiom of Quantified Negation.  This axiom is used to manipulate negated
     quantifiers.  One of the 4 axioms of pure predicate calculus.  Equivalent
     to axiom scheme C7' in [Megill] p. 448 (p. 16 of the preprint).  Another
     equivalent variant ~ ax6 appears as Axiom C5-2 of [Monk2] p. 113.  $)
  ax-6 $a |- ( -. A. x -. A. x ph -> ph ) $.

  $( Axiom of Quantifier Commutation.  This axiom says universal quantifiers
     can be swapped.  One of the 4 axioms of pure predicate calculus.  Axiom
     scheme C6' in [Megill] p. 448 (p. 16 of the preprint).  Also appears as
     Lemma 12 of [Monk2] p. 109. $)
  ax-7 $a |- ( A. x A. y ph -> A. y A. x ph ) $.

  ${
    ax-g.1 $e |- ph $.
    $( Rule of Generalization.  The postulated inference rule of pure predicate
       calculus.  See e.g. Rule 2 of [Hamilton] p. 74.  This rule says that if
       something is unconditionally true, then it is true for all values of a
       variable.  For example, if we have proved ` x = x ` , we can conclude
       ` A. x x = x ` or even ` A. y x = x ` .  Theorem ~ a4i shows we can go
      the other way also:  in other words we can add or remove universal
      quantifiers from the beginning of any theorem as required. $)
    ax-gen $a |- A. x ph $.
  $}


  $( Declare the existential quantifier symbol. $)
  $c E. $.    $( Backwards E (read:  'there exists') $)

  $( Extend wff definition to include the existential quantifier ("there
     exists"). $)
  wex $a wff E. x ph $.

  $( Define existential quantification.  ` E. x ph ` means "there exists at
     least one set ` x ` such that ` ph ` is true."  Definition of [Margaris]
     p. 49.  $)
  df-ex $a |- ( E. x ph <-> -. A. x -. ph ) $.

  ${
    a4i.1 $e |- A. x ph $.
    $( Inference rule reversing generalization. $)
    a4i $p |- ph $=
      ( wal ax-4 ax-mp ) ABDACABEF $.
      $( [5-Aug-93] $)
  $}

  ${
    gen2.1 $e |- ph $.
    $( Generalization applied twice. $)
    gen2 $p |- A. x A. y ph $=
      ( wal ax-gen ) ACEBACDFF $.
      $( [30-Apr-98] $)
  $}

  ${
    a4s.1 $e |- ( ph -> ps ) $.
    $( Generalization of antecedent. $)
    a4s $p |- ( A. x ph -> ps ) $=
      ( wal ax-4 syl ) ACEABACFDG $.
      $( [5-Aug-93] $)
  $}

  ${
    a4sd.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction generalizing antecedent. $)
    a4sd $p |- ( ph -> ( A. x ps -> ch ) ) $=
      ( wal ax-4 syl5 ) ABCBDFEBDGH $.
      $( [17-Aug-94] $)
  $}

  ${
    mpg.1 $e |- ( A. x ph -> ps ) $.
    mpg.2 $e |- ph $.
    $( Modus ponens combined with generalization. $)
    mpg $p |- ps $=
      ( wal ax-gen ax-mp ) ACFBACEGDH $.
      $( [24-May-94] $)
  $}

  ${
    mpgbi.1 $e |- ( A. x ph <-> ps ) $.
    mpgbi.2 $e |- ph $.
    $( Modus ponens on biconditional combined with generalization. $)
    mpgbi $p |- ps $=
      ( wal biimp mpg ) ABCACFBDGEH $.
      $( [24-May-94] $)
  $}

  ${
    mpgbir.1 $e |- ( ph <-> A. x ps ) $.
    mpgbir.2 $e |- ps $.
    $( Modus ponens on biconditional combined with generalization. $)
    mpgbir $p |- ph $=
      ( wal biimpr mpg ) BACABCFDGEH $.
      $( [24-May-94] $)
  $}

  ${
    a5i.1 $e |- ( A. x ph -> ps ) $.
    $( Inference from ~ ax-5 . $)
    a5i $p |- ( A. x ph -> A. x ps ) $=
      ( wal wi ax-5 mpg ) ACEZBFIBCEFCABCGDH $.
      $( [5-Aug-93] $)
  $}

  $( Abbreviated version of ~ ax-6 . $)
  a6e $p |- ( E. x A. x ph -> ph ) $=
    ( wal wex wn df-ex ax-6 sylbi ) ABCZBDIEBCEAIBFABGH $.
    $( [5-Aug-93] $)

  ${
    a7s.1 $e |- ( A. x A. y ph -> ps ) $.
    $( Swap quantifiers in an antecedent. $)
    a7s $p |- ( A. y A. x ph -> ps ) $=
      ( wal ax-7 syl ) ACFDFADFCFBADCGEH $.
      $( [5-Aug-93] $)
  $}

  $( Theorem 19.20 of [Margaris] p. 90. $)
  19.20 $p |- ( A. x ( ph -> ps ) -> ( A. x ph -> A. x ps ) ) $=
    ( wi wal ax-4 syl4 a4s a5i ax-5 syl ) ABDZCEACEZBDZCEMBCEDLNCLNCMABACFGHIAB
    CJK $.
    $( [5-Aug-93] $)

  ${
    19.20i.1 $e |- ( ph -> ps ) $.
    $( Inference quantifying both antecedent and consequent. $)
    19.20i $p |- ( A. x ph -> A. x ps ) $=
      ( a4s a5i ) ABCABCDEF $.
      $( [5-Aug-93] $)
  $}

  ${
    19.20ii.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Inference quantifying antecedent, nested antecedent, and consequent. $)
    19.20ii $p |- ( A. x ph -> ( A. x ps -> A. x ch ) ) $=
      ( wal wi 19.20i 19.20 syl ) ADFBCGZDFBDFCDFGAKDEHBCDIJ $.
      $( [5-Aug-93] $)
  $}

  ${
    19.20d.1 $e |- ( ph -> A. x ph ) $.
    19.20d.2 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.20 of [Margaris] p. 90. $)
    19.20d $p |- ( ph -> ( A. x ps -> A. x ch ) ) $=
      ( wal wi 19.20ii syl ) AADGBDGCDGHEABCDFIJ $.
      $( [4-Jan-02] $)
  $}

  $( Theorem 19.15 of [Margaris] p. 90. $)
  19.15 $p |- ( A. x ( ph <-> ps ) -> ( A. x ph <-> A. x ps ) ) $=
    ( wb wal bi1 19.20ii bi2 impbid ) ABDZCEACEBCEJABCABFGJBACABHGI $.
    $( [5-Aug-93] $)

  ${
    bial.1 $e |- ( ph <-> ps ) $.
    $( Inference adding universal quantifier to both sides of an
       equivalence. $)
    bial $p |- ( A. x ph <-> A. x ps ) $=
      ( wb wal 19.15 mpg ) ABEACFBCFECABCGDH $.
      $( [7-Aug-94] $)

    $( Inference adding 2 universal quantifiers to both sides of an
       equivalence. $)
    bi2al $p |- ( A. x A. y ph <-> A. x A. y ps ) $=
      ( wal bial ) ADFBDFCABDEGG $.
      $( [9-Mar-97] $)
  $}


  $( The following "hypothesis-building" lemmas allow us to construct proofs of
     formulas of the form ` |- ( ph -> A. x ph ) ` from smaller formulas of
     this form.  These are useful for constructing hypotheses that state " ` x `
     is (effectively) not free in ` ph ` ". $)

  ${
    hbth.1 $e |- ph $.
    $( No variable is (effectively) free in a theorem. $)
    hbth $p |- ( ph -> A. x ph ) $=
      ( wal ax-gen a1i ) ABDAABCEF $.
      $( [5-Aug-93] $)
  $}

  $( ` x ` is not free in ` A. x ph ` .  Appendix example in [Megill] p. 450
     (p. 19 of the preprint). $)
  hba1 $p |- ( A. x ph -> A. x A. x ph ) $=
    ( wal id a5i ) AABCZBFDE $.
    $( [5-Aug-93] $)

  ${
    hb.1 $e |- ( ph -> A. x ph ) $.
    $( If ` x ` is not free in ` ph ` , it is not free in ` -. ph ` . $)
    hbne $p |- ( -. ph -> A. x -. ph ) $=
      ( wal wn con3i 19.20i ax-6 nsyl4 ) ABDZEZBDAEZBDAKLBAJCFGABHI $.
      $( [5-Aug-93] $)

    $( If ` x ` is not free in ` ph ` , it is not free in ` A. y ph ` . $)
    hbal $p |- ( A. y ph -> A. x A. y ph ) $=
      ( wal 19.20i ax-7 syl ) ACEZABEZCEIBEAJCDFACBGH $.
      $( [5-Aug-93] $)

    $( If ` x ` is not free in ` ph ` , it is not free in ` E. y ph ` . $)
    hbex $p |- ( E. y ph -> A. x E. y ph ) $=
      ( wn wal wex hbne hbal df-ex bial 3imtr4 ) AEZCFZEZOBFACGZPBFNBMBCABDHIHA
      CJZPOBQKL $.
      $( [5-Aug-93] $)
    ${

      hb.2 $e |- ( ps -> A. x ps ) $.
      $( If ` x ` is not free in ` ph ` and ` ps ` , it is not free in
         ` ( ph -> ps ) ` . $)
      hbim $p |- ( ( ph -> ps ) -> A. x ( ph -> ps ) ) $=
        ( wi wal wn hbne pm2.21 19.20i syl ax-1 ja ) ABABFZCGZAHZQCGPACDIQOCABJ
        KLBBCGPEBOCBAMKLN $.
        $( [5-Aug-93] $)

      $( If ` x ` is not free in ` ph ` and ` ps ` , it is not free in
         ` ( ph \/ ps ) ` . $)
      hbor $p |- ( ( ph \/ ps ) -> A. x ( ph \/ ps ) ) $=
        ( wn wi wal wo hbne hbim df-or bial 3imtr4 ) AFZBGZPCHABIZQCHOBCACDJEKA
        BLZQPCRMN $.
        $( [5-Aug-93] $)

      $( If ` x ` is not free in ` ph ` and ` ps ` , it is not free in
         ` ( ph /\ ps ) ` . $)
      hban $p |- ( ( ph /\ ps ) -> A. x ( ph /\ ps ) ) $=
        ( wn wi wal wa hbne hbim df-an bial 3imtr4 ) ABFZGZFZQCHABIZRCHPCAOCDBC
        EJKJABLZRQCSMN $.
        $( [5-Aug-93] $)

      $( If ` x ` is not free in ` ph ` and ` ps ` , it is not free in
         ` ( ph <-> ps ) ` . $)
      hbbi $p |- ( ( ph <-> ps ) -> A. x ( ph <-> ps ) ) $=
        ( wi wa wal wb hbim hban bi bial 3imtr4 ) ABFZBAFZGZQCHABIZRCHOPCABCDEJ
        BACEDJKABLZRQCSMN $.
        $( [5-Aug-93] $)

      ${

        hb.3 $e |- ( ch -> A. x ch ) $.
        $( If ` x ` is not free in ` ph ` , ` ps ` , and ` ch ` , it is not
           free in ` ( ph \/ ps \/ ch ) ` . $)
        hb3or $p |- ( ( ph \/ ps \/ ch ) -> A. x ( ph \/ ps \/ ch ) ) $=
          ( wo wal w3o hbor df-3or bial 3imtr4 ) ABHZCHZPDIABCJZQDIOCDABDEFKGKA
          BCLZQPDRMN $.
          $( [14-Sep-03] $)

        $( If ` x ` is not free in ` ph ` , ` ps ` , and ` ch ` , it is not
           free in ` ( ph /\ ps /\ ch ) ` . $)
        hb3an $p |- ( ( ph /\ ps /\ ch ) -> A. x ( ph /\ ps /\ ch ) ) $=
          ( wa wal w3a hban df-3an bial 3imtr4 ) ABHZCHZPDIABCJZQDIOCDABDEFKGKA
          BCLZQPDRMN $.
          $( [14-Sep-03] $)
      $}
    $}
  $}

  $( ` x ` is not free in ` -. A. x ph ` . $)
  hbn1 $p |- ( -. A. x ph -> A. x -. A. x ph ) $=
    ( wal hba1 hbne ) ABCBABDE $.
    $( [5-Aug-93] $)

  $( ` x ` is not free in ` E. x ph ` . $)
  hbe1 $p |- ( E. x ph -> A. x E. x ph ) $=
    ( wn wal wex hbn1 df-ex bial 3imtr4 ) ACZBDCZKBDABEZLBDJBFABGZLKBMHI $.
    $( [5-Aug-93] $)

  $( A closed form of hypothesis builder ~ hbne . $)
  hbnt $p |- ( A. x ( ph -> A. x ph ) -> ( -. ph -> A. x -. ph ) ) $=
    ( wal wi wn con3 19.20ii ax-6 con1i syl5 ) AABCZDZBCKEZBCZAEZBCOLMOBAKFGNAA
    BHIJ $.
    $( [5-Aug-93] $)

  $( Axiom C5-2 of [Monk2] p. 113, which we prove from our ~ ax-6 (and others).
     Conversely, ~ ax-6 follows from this using ~ ax-4 and propositional
     calculus, showing that they are interchangeable.   $)
  ax6 $p |- ( -. A. x ph -> A. x -. A. x ph ) $=
    ( hbn1 ) ABC $.
    $( [2-Dec-03] $) $( [2-Dec-03] $)

  $( If a wff is true, it is true for at least one instance. $)
  19.8a $p |- ( ph -> E. x ph ) $=
    ( wn wal wex ax-4 con2i df-ex sylibr ) AACZBDZCABEKAJBFGABHI $.
    $( [5-Aug-93] $)

  $( Theorem 19.2 of [Margaris] p. 89. $)
  19.2 $p |- ( A. x ph -> E. x ph ) $=
    ( wex 19.8a a4s ) AABCBABDE $.
    $( [5-Aug-93] $)

  ${
    19.3r.1 $e |- ( ph -> A. x ph ) $.
    $( A wff may be quantified with a variable not free in it. $)
    19.3r $p |- ( ph <-> A. x ph ) $=
      ( wal ax-4 impbi ) AABDCABEF $.
      $( [5-Aug-93] $)
  $}

  $( Theorem 19.5 of [Margaris] p. 89. $)
  alcom $p |- ( A. x A. y ph <-> A. y A. x ph ) $=
    ( wal ax-7 impbi ) ACDBDABDCDABCEACBEF $.
    $( [5-Aug-93] $)

  $( Theorem 19.7 of [Margaris] p. 89. $)
  alnex $p |- ( A. x -. ph <-> -. E. x ph ) $=
    ( wex wn wal df-ex bicon2 ) ABCADBEABFG $.
    $( [5-Aug-93] $)

  $( Theorem 19.6 of [Margaris] p. 89. $)
  alex $p |- ( A. x ph <-> -. E. x -. ph ) $=
    ( wal wn wex pm4.13 bial alnex bitr ) ABCADZDZBCJBEDAKBAFGJBHI $.
    $( [5-Aug-93] $)

  ${
    19.9r.1 $e |- ( ph -> A. x ph ) $.
    $( Variation of Theorem 19.9 of [Margaris] p. 89. $)
    19.9r $p |- ( ph <-> E. x ph ) $=
      ( wex 19.8a wn wal df-ex con3i 19.20i ax-6 syl sylbi impbi ) AABDZABEOAFZ
      BGZFZAABHRABGZFZBGZFAUAQTPBASCIJIABKLMN $.
      $( [5-Aug-93] $)
  $}

  $( A closed version of one direction of ~ 19.9r . $)
  19.9t $p |- ( A. x ( ph -> A. x ph ) -> ( E. x ph -> ph ) ) $=
    ( wal wi wn wex hbnt con1d df-ex syl5ib ) AABCDBCZAEBCZEAABFKALABGHABIJ $.
    $( [5-Aug-93] $)

  ${
    19.9d.1 $e |- ( ps -> A. x ps ) $.
    19.9d.2 $e |- ( ps -> ( ph -> A. x ph ) ) $.
    $( A deduction version of one direction of ~ 19.9r . $)
    19.9d $p |- ( ps -> ( E. x ph -> ph ) ) $=
      ( wal wi wex 19.20i 19.9t 3syl ) BBCFAACFGZCFACHAGDBLCEIACJK $.
      $( [5-Aug-93] $)
  $}

  $( Theorem 19.14 of [Margaris] p. 90. $)
  exnal $p |- ( E. x -. ph <-> -. A. x ph ) $=
    ( wal wn wex alex bicon2 ) ABCADBEABFG $.
    $( [5-Aug-93] $)

  $( Theorem 19.22 of [Margaris] p. 90. $)
  19.22 $p |- ( A. x ( ph -> ps ) -> ( E. x ph -> E. x ps ) ) $=
    ( wi wal wn wex con3 19.20ii con3d df-ex 3imtr4g ) ABDZCEZAFZCEZFBFZCEZFACG
    BCGNRPMQOCABHIJACKBCKL $.
    $( [5-Aug-93] $)

  ${
    19.22i.1 $e |- ( ph -> ps ) $.
    $( Inference adding existential quantifier to antecedent and consequent. $)
    19.22i $p |- ( E. x ph -> E. x ps ) $=
      ( wi wex 19.22 mpg ) ABEACFBCFECABCGDH $.
      $( [5-Aug-93] $)
  $}

  $( A transformation of quantifiers and logical connectives. $)
  alinexa $p |- ( A. x ( ph -> -. ps ) <-> -. E. x ( ph /\ ps ) ) $=
    ( wn wi wal wa wex imnan bial alnex bitr ) ABDEZCFABGZDZCFNCHDMOCABIJNCKL $.
    $( [19-Aug-93] $)

  $( A transformation of quantifiers and logical connectives. $)
  exanali $p |- ( E. x ( ph /\ -. ps ) <-> -. A. x ( ph -> ps ) ) $=
    ( wi wal wn wa wex iman bial alnex bitr bicon2 ) ABDZCEZABFGZCHZOPFZCEQFNRCA
    BIJPCKLM $.
    $( [25-Mar-96] $)

  $( A relationship between two quantifiers and negation. $)
  alexn $p |- ( A. x E. y -. ph <-> -. E. x A. y ph ) $=
    ( wn wex wal exnal bial alnex bitr ) ADCEZBFACFZDZBFLBEDKMBACGHLBIJ $.
    $( [18-Aug-93] $)

  $( One direction of Theorem 19.11 of [Margaris] p. 89. $)
  excomim $p |- ( E. x E. y ph -> E. y E. x ph ) $=
    ( wex 19.8a 19.22i hbe1 hbex 19.9r sylibr ) ACDZBDABDZCDZBDMKMBALCABEFFMBLB
    CABGHIJ $.
    $( [5-Aug-93] $)

  $( Theorem 19.11 of [Margaris] p. 89. $)
  excom $p |- ( E. x E. y ph <-> E. y E. x ph ) $=
    ( wex excomim impbi ) ACDBDABDCDABCEACBEF $.
    $( [5-Aug-93] $)

  $( Theorem 19.12 of [Margaris] p. 89. Assuming the converse is a mistake
     sometimes made by beginners!   But sometimes the converse does hold,
     as in ~ 19.12vv . $)
  19.12 $p |- ( E. x A. y ph -> A. y E. x ph ) $=
    ( wal wex hba1 hbex ax-4 19.22i 19.20i syl ) ACDZBEZMCDABEZCDLCBACFGMNCLABA
    CHIJK $.
    $( [5-Aug-93] $)

  ${
    19.16.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.16 of [Margaris] p. 90. $)
    19.16 $p |- ( A. x ( ph <-> ps ) -> ( ph <-> A. x ps ) ) $=
      ( wb wal 19.15 19.3r syl5bb ) ABECFACFBCFAABCGACDHI $.
      $( [5-Aug-93] $)
  $}

  ${
    19.17.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.17 of [Margaris] p. 90. $)
    19.17 $p |- ( A. x ( ph <-> ps ) -> ( A. x ph <-> ps ) ) $=
      ( wb wal 19.15 19.3r syl6bbr ) ABECFACFBCFBABCGBCDHI $.
      $( [5-Aug-93] $)
  $}

  $( Theorem 19.18 of [Margaris] p. 90. $)
  19.18 $p |- ( A. x ( ph <-> ps ) -> ( E. x ph <-> E. x ps ) ) $=
    ( wb wal wex wi bi1 19.20i 19.22 syl bi2 impbid ) ABDZCEZACFZBCFZOABGZCEPQG
    NRCABHIABCJKOBAGZCEQPGNSCABLIBACJKM $.
    $( [5-Aug-93] $)

  ${
    biex.1 $e |- ( ph <-> ps ) $.
    $( Inference adding existential quantifier to both sides of an
       equivalence. $)
    biex $p |- ( E. x ph <-> E. x ps ) $=
      ( wb wex 19.18 mpg ) ABEACFBCFECABCGDH $.
      $( [24-May-94] $)
  $}

  ${
    bi2ex.1 $e |- ( ph <-> ps ) $.
    $( Inference adding 2 existential quantifiers to both sides of an
       equivalence. $)
    bi2ex $p |- ( E. x E. y ph <-> E. x E. y ps ) $=
      ( wex biex ) ADFBDFCABDEGG $.
      $( [16-Mar-95] $)
  $}

  ${
    bi3ex.1 $e |- ( ph <-> ps ) $.
    $( Inference adding 3 existential quantifiers to both sides of an
       equivalence. $)
    bi3ex $p |- ( E. x E. y E. z ph <-> E. x E. y E. z ps ) $=
      ( wex biex bi2ex ) AEGBEGCDABEFHI $.
      $( [2-May-95] $)
  $}

  $( Commutation of conjunction inside an existential quantifier. $)
  exancom $p |- ( E. x ( ph /\ ps ) <-> E. x ( ps /\ ph ) ) $=
    ( wa ancom biex ) ABDBADCABEF $.
    $( [18-Aug-93] $)

  ${
    19.19.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.19 of [Margaris] p. 90. $)
    19.19 $p |- ( A. x ( ph <-> ps ) -> ( ph <-> E. x ps ) ) $=
      ( wb wal wex 19.18 19.9r syl5bb ) ABECFACGBCGAABCHACDIJ $.
      $( [5-Aug-93] $)
  $}

  ${
    19.21.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.21 of [Margaris] p. 90.  The hypothesis can be thought of as
       " ` x ` is not free in ` ph ` ". $)
    19.21 $p |- ( A. x ( ph -> ps ) <-> ( ph -> A. x ps ) ) $=
      ( wi wal 19.20 syl5 hba1 hbim ax-4 syl3 19.20i syl impbi ) ABEZCFZABCFZEZ
      QACFRAABCGDHSSCFQARCDBCIJSPCRBABCKLMNO $.
      $( [5-Aug-93] $)
  $}

  ${
    stdpc5.1 $e |- ( ph -> A. x ph ) $.
    $( An axiom of standard predicate calculus.  Axiom 5 of [Mendelson] p. 59.
       The hypothesis ` ( ph -> A. x ph ) ` can be thought of as " ` x ` is not
       free in ` ph ` ".  With this convention, the meaning of "not free" is
       less restrictive than the usual textbook definition; for example ` x `
       would not (for us) be free in ` x = x ` since (by ~ eqid and ~ hbth ) we
       can prove ` ( x = x -> A. x x = x ) ` . $)
    stdpc5 $p |- ( A. x ( ph -> ps ) -> ( ph -> A. x ps ) ) $=
      ( wi wal 19.21 biimp ) ABECFABCFEABCDGH $.
      $( [22-Sep-93] $)
  $}

  ${
    19.21ai.1 $e |- ( ph -> A. x ph ) $.
    19.21ai.2 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90. $)
    19.21ai $p |- ( ph -> A. x ps ) $=
      ( wal 19.20i syl ) AACFBCFDABCEGH $.
      $( [5-Aug-93] $)
  $}

  ${
    19.21ad.1 $e |- ( ph -> A. x ph ) $.
    19.21ad.2 $e |- ( ps -> A. x ps ) $.
    19.21ad.3 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.21 of [Margaris] p. 90. $)
    19.21ad $p |- ( ph -> ( ps -> A. x ch ) ) $=
      ( wal wa hban imp 19.21ai exp ) ABCDHABICDABDEFJABCGKLM $.
      $( [10-Feb-97] $)
  $}

  ${
    19.21bi.1 $e |- ( ph -> A. x ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90. $)
    19.21bi $p |- ( ph -> ps ) $=
      ( wal ax-4 syl ) ABCEBDBCFG $.
      $( [5-Aug-93] $)
  $}

  ${
    19.21bbi.1 $e |- ( ph -> A. x A. y ps ) $.
    $( Inference removing double quantifier. $)
    19.21bbi $p |- ( ph -> ps ) $=
      ( wal 19.21bi ) ABDABDFCEGG $.
      $( [20-Apr-94] $)
  $}

  ${
    19.22d.1 $e |- ( ph -> A. x ph ) $.
    19.22d.2 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.22 of [Margaris] p. 90. $)
    19.22d $p |- ( ph -> ( E. x ps -> E. x ch ) ) $=
      ( wi wal wex 19.21ai 19.22 syl ) ABCGZDHBDICDIGAMDEFJBCDKL $.
      $( [20-May-96] $)
  $}

  ${
    19.23.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.23 of [Margaris] p. 90. $)
    19.23 $p |- ( A. x ( ph -> ps ) <-> ( E. x ph -> ps ) ) $=
      ( wi wal wex 19.22 19.9r syl6ibr hbe1 hbim 19.8a syl4 19.21ai impbi ) ABE
      ZCFZACGZBEZRSBCGBABCHBCDIJTQCSBCACKDLASBACMNOP $.
      $( [5-Aug-93] $)
  $}

  ${
    19.23ai.1 $e |- ( ps -> A. x ps ) $.
    19.23ai.2 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90. $)
    19.23ai $p |- ( E. x ph -> ps ) $=
      ( wex 19.22i 19.9r sylibr ) ACFBCFBABCEGBCDHI $.
      $( [5-Aug-93] $)
  $}

  ${
    19.23bi.1 $e |- ( E. x ph -> ps ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90. $)
    19.23bi $p |- ( ph -> ps ) $=
      ( wex 19.8a syl ) AACEBACFDG $.
      $( [5-Aug-93] $)
  $}

  ${
    19.23ad.1 $e |- ( ph -> A. x ph ) $.
    19.23ad.2 $e |- ( ch -> A. x ch ) $.
    19.23ad.3 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.23 of [Margaris] p. 90. $)
    19.23ad $p |- ( ph -> ( E. x ps -> ch ) ) $=
      ( wi wal wex 19.21ai 19.23 sylib ) ABCHZDIBDJCHANDEGKBCDFLM $.
      $( [28-Jan-97] $)
  $}

  $( Theorem 19.26 of [Margaris] p. 90. $)
  19.26 $p |- ( A. x ( ph /\ ps ) <-> ( A. x ph /\ A. x ps ) ) $=
    ( wa wal pm3.26 19.20i pm3.27 jca pm3.2 19.20ii imp impbi ) ABDZCEZACEZBCEZ
    DOPQNACABFGNBCABHGIPQOABNCABJKLM $.
    $( [5-Aug-93] $)

  ${
    19.27.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.27 of [Margaris] p. 90. $)
    19.27 $p |- ( A. x ( ph /\ ps ) <-> ( A. x ph /\ ps ) ) $=
      ( wa wal 19.26 19.3r bilan bitr4 ) ABECFACFZBCFZEKBEABCGBLKBCDHIJ $.
      $( [5-Aug-93] $)
  $}

  ${
    19.28.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.28 of [Margaris] p. 90. $)
    19.28 $p |- ( A. x ( ph /\ ps ) <-> ( ph /\ A. x ps ) ) $=
      ( wa wal 19.26 19.3r biran bitr4 ) ABECFACFZBCFZEALEABCGAKLACDHIJ $.
      $( [5-Aug-93] $)
  $}

  $( Theorem 19.29 of [Margaris] p. 90. $)
  19.29 $p |- ( ( A. x ph /\ E. x ps ) -> E. x ( ph /\ ps ) ) $=
    ( wal wex wa wn wi 19.20 alnex syl6ib con3i df-an exnal 3imtr4 biex sylibr
    ) ACDZBCEZFZABGZHZGZCEZABFZCERSGZHZGUBCDZGTUDUHUGUHRUACDUFAUACIBCJKLRSMUBCN
    OUEUCCABMPQ $.
    $( [5-Aug-93] $)

  $( Variation of Theorem 19.29 of [Margaris] p. 90. $)
  19.29r $p |- ( ( E. x ph /\ A. x ps ) -> E. x ( ph /\ ps ) ) $=
    ( wal wex wa 19.29 ancom exancom 3imtr4 ) BCDZACEZFBAFCELKFABFCEBACGLKHABCI
    J $.
    $( [18-Aug-93] $)

  $( Theorem 19.35 of [Margaris] p. 90.  This theorem is useful for binding
     an implication (in the form of the right-hand side) into the scope of a
     single existential quantifier.  $)
  19.35 $p |- ( E. x ( ph -> ps ) <-> ( A. x ph -> E. x ps ) ) $=
    ( wal wn wi wex wa 19.26 annim bial df-an 3bitr3 bicon2 df-ex bilim
    3bitr4r ) ACDZBEZCDZEZFZABFZEZCDZERBCGZFUCCGUEUBASHZCDRTHUEUBEASCIUGUDCABJK
    RTLMNUFUARBCOPUCCOQ $.
    $( [5-Aug-93] $)

  ${
    19.35i.1 $e |- E. x ( ph -> ps ) $.
    $( Inference from Theorem 19.35 of [Margaris] p. 90. $)
    19.35i $p |- ( A. x ph -> E. x ps ) $=
      ( wi wex wal 19.35 mpbi ) ABECFACGBCFEDABCHI $.
      $( [5-Aug-93] $)
  $}

  ${
    19.35ri.1 $e |- ( A. x ph -> E. x ps ) $.
    $( Inference from Theorem 19.35 of [Margaris] p. 90. $)
    19.35ri $p |- E. x ( ph -> ps ) $=
      ( wi wex wal 19.35 mpbir ) ABECFACGBCFEDABCHI $.
      $( [5-Aug-93] $)
  $}

  ${
    19.36.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.36 of [Margaris] p. 90. $)
    19.36 $p |- ( E. x ( ph -> ps ) <-> ( A. x ph -> ps ) ) $=
      ( wi wex wal 19.35 19.9r bilim bitr4 ) ABECFACGZBCFZELBEABCHBMLBCDIJK $.
      $( [5-Aug-93] $)
  $}

  ${
    19.36i.1 $e |- ( ps -> A. x ps ) $.
    19.36i.2 $e |- E. x ( ph -> ps ) $.
    $( Inference from Theorem 19.36 of [Margaris] p. 90. $)
    19.36i $p |- ( A. x ph -> ps ) $=
      ( wi wex wal 19.36 mpbi ) ABFCGACHBFEABCDIJ $.
      $( [5-Aug-93] $)
  $}

  ${
    19.37.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.37 of [Margaris] p. 90. $)
    19.37 $p |- ( E. x ( ph -> ps ) <-> ( ph -> E. x ps ) ) $=
      ( wi wex wal 19.35 19.3r birim bitr4 ) ABECFACGZBCFZEAMEABCHALMACDIJK $.
      $( [5-Aug-93] $)
  $}

  $( Theorem 19.38 of [Margaris] p. 90. $)
  19.38 $p |- ( ( E. x ph -> A. x ps ) -> A. x ( ph -> ps ) ) $=
    ( wex wal wi hbe1 hba1 hbim 19.8a ax-4 syl34 19.21ai ) ACDZBCEZFABFCNOCACGB
    CHIANOBACJBCKLM $.
    $( [5-Aug-93] $)

  $( Theorem 19.39 of [Margaris] p. 90. $)
  19.39 $p |- ( ( E. x ph -> E. x ps ) -> E. x ( ph -> ps ) ) $=
    ( wex wi wal 19.2 syl4 19.35 sylibr ) ACDZBCDZEACFZLEABECDMKLACGHABCIJ $.
    $( [5-Aug-93] $)

  $( Theorem 19.24 of [Margaris] p. 90. $)
  19.24 $p |- ( ( A. x ph -> A. x ps ) -> E. x ( ph -> ps ) ) $=
    ( wal wi wex 19.2 syl3 19.35 sylibr ) ACDZBCDZEKBCFZEABECFLMKBCGHABCIJ $.
    $( [5-Aug-93] $)

  $( Theorem 19.25 of [Margaris] p. 90. $)
  19.25 $p |- ( A. y E. x ( ph -> ps ) ->
              ( E. y A. x ph -> E. y E. x ps ) ) $=
    ( wi wex wal 19.35 biimp 19.20i 19.22 syl ) ABECFZDGACGZBCFZEZDGNDFODFEMPDM
    PABCHIJNODKL $.
    $( [5-Aug-93] $)

  $( Theorem 19.30 of [Margaris] p. 90. $)
  19.30 $p |- ( A. x ( ph \/ ps ) -> ( A. x ph \/ E. x ps ) ) $=
    ( wn wi wal wo wex 19.20 orcom df-or bitr bial df-ex bilor imor 3bitr4
    3imtr4 ) BDZAEZCFSCFZACFZEZABGZCFUBBCHZGZSACIUDTCUDBAGTABJBAKLMUBUADZGUGUBG
    UFUCUBUGJUEUGUBBCNOUAUBPQR $.
    $( [5-Aug-93] $)

  ${
    19.32.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.32 of [Margaris] p. 90. $)
    19.32 $p |- ( A. x ( ph \/ ps ) <-> ( ph \/ A. x ps ) ) $=
      ( wn wi wal wo hbne 19.21 df-or bial 3bitr4 ) AEZBFZCGNBCGZFABHZCGAPHNBCA
      CDIJQOCABKLAPKM $.
      $( [5-Aug-93] $)
  $}

  ${
    19.31.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.31 of [Margaris] p. 90. $)
    19.31 $p |- ( A. x ( ph \/ ps ) <-> ( A. x ph \/ ps ) ) $=
      ( wo wal 19.32 orcom bial 3bitr4 ) BAEZCFBACFZEABEZCFLBEBACDGMKCABHILBHJ
      $.
      $( [5-Aug-93] $)
  $}

  $( Theorem 19.43 of [Margaris] p. 90. $)
  19.43 $p |- ( E. x ( ph \/ ps ) <-> ( E. x ph \/ E. x ps ) ) $=
    ( wo wn wal wex wa ioran bial 19.26 alnex bi2an 3bitr bineg df-ex oran
    3bitr4 ) ABDZEZCFZEACGZEZBCGZEZHZESCGUBUDDUAUFUAAEZBEZHZCFUGCFZUHCFZHUFTUIC
    ABIJUGUHCKUJUCUKUEACLBCLMNOSCPUBUDQR $.
    $( [5-Aug-93] $)

  ${
    19.44.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.44 of [Margaris] p. 90. $)
    19.44 $p |- ( E. x ( ph \/ ps ) <-> ( E. x ph \/ ps ) ) $=
      ( wo wex 19.43 19.9r bilor bitr4 ) ABECFACFZBCFZEKBEABCGBLKBCDHIJ $.
      $( [5-Aug-93] $)
  $}

  ${
    19.45.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.45 of [Margaris] p. 90. $)
    19.45 $p |- ( E. x ( ph \/ ps ) <-> ( ph \/ E. x ps ) ) $=
      ( wo wex 19.43 19.9r biror bitr4 ) ABECFACFZBCFZEALEABCGAKLACDHIJ $.
      $( [5-Aug-93] $)
  $}

  $( Theorem 19.33 of [Margaris] p. 90. $)
  19.33 $p |- ( ( A. x ph \/ A. x ps ) -> A. x ( ph \/ ps ) ) $=
    ( wal wo orc 19.20i olc jaoi ) ACDABEZCDBCDAJCABFGBJCBAHGI $.
    $( [5-Aug-93] $)

  $( The antecedent provides a condition implying the converse of ~ 19.33 .
     Compare Theorem 19.33 of [Margaris] p. 90. $)
  19.33b $p |- ( -. ( E. x ph /\ E. x ps ) ->
               (  A. x ( ph \/ ps ) <-> ( A. x ph \/ A. x ps ) ) ) $=
    ( wex wa wn wo wal wi ianor alnex bi2or bitr4 wb biorf 19.20i 19.15 syl olc
    syl6bir orcom syl6bb orc jaoi sylbi 19.33 a1i impbid ) ACDZBCDZEFZABGZCHZAC
    HZBCHZGZUKAFZCHZBFZCHZGZUMUPIZUKUIFZUJFZGVAUIUJJURVCUTVDACKBCKLMURVBUTURUMU
    OUPURBULNZCHUOUMNUQVECABOPBULCQRUOUNSTUTUMUNUPUTAULNZCHUNUMNUSVFCUSABAGULBA
    OBAUAUBPAULCQRUNUOUCTUDUEUPUMIUKABCUFUGUH $.
    $( [2-Apr-04] $) $( [27-Mar-04] $)

  $( Theorem 19.34 of [Margaris] p. 90. $)
  19.34 $p |- ( ( A. x ph \/ E. x ps ) -> E. x ( ph \/ ps ) ) $=
    ( wal wex wo 19.2 imror 19.43 sylibr ) ACDZBCEZFACEZLFABFCEKMLACGHABCIJ $.
    $( [5-Aug-93] $)

  $( Theorem 19.40 of [Margaris] p. 90. $)
  19.40 $p |- ( E. x ( ph /\ ps ) -> ( E. x ph /\ E. x ps ) ) $=
    ( wa wex pm3.26 19.22i pm3.27 jca ) ABDZCEACEBCEJACABFGJBCABHGI $.
    $( [5-Aug-93] $)

  ${
    19.41.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.41 of [Margaris] p. 90. $)
    19.41 $p |- ( E. x ( ph /\ ps ) <-> ( E. x ph /\ ps ) ) $=
      ( wa wex wn wal df-ex wo hbne 19.31 ianor bial alnex biror bitr4 3bitr4
      bicon2 ) ABEZCFTGZCHZGACFZBEZTCIUBUDAGZBGZJZCHUECHZUFJZUBUDGZUEUFCBCDKLUA
      UGCABMNUJUCGZUFJUIUCBMUHUKUFACOPQRSQ $.
      $( [5-Aug-93] $)
  $}

  ${
    19.42.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.42 of [Margaris] p. 90. $)
    19.42 $p |- ( E. x ( ph /\ ps ) <-> ( ph /\ E. x ps ) ) $=
      ( wa wex 19.41 exancom ancom 3bitr4 ) BAECFBCFZAEABECFAKEBACDGABCHAKIJ $.
      $( [18-Aug-93] $)
  $}

  $( Swap 1st and 3rd existential quantifiers. $)
  excom13 $p |- ( E. x E. y E. z ph <-> E. z E. y E. x ph ) $=
    ( wex excom biex 3bitr ) ADEZCEBEIBEZCEABEZDEZCEKCEDEIBCFJLCABDFGKCDFH $.
    $( [9-Mar-95] $)

  $( Rotate existential quantifiers. $)
  exrot3 $p |- ( E. x E. y E. z ph <-> E. y E. z E. x ph ) $=
    ( wex excom13 excom bitr ) ADECEBEABEZCEDEIDECEABCDFIDCGH $.
    $( [17-Mar-95] $)

  $( Rotate existential quantifiers twice. $)
  exrot4 $p |- ( E. x E. y E. z E. w ph <-> E. z E. w E. x E. y ph ) $=
    ( wex excom13 biex bitr ) AEFDFCFZBFACFZDFEFZBFKBFEFDFJLBACDEGHKBEDGI $.
    $( [9-Mar-95] $)

  ${
    nex.1 $e |- -. ph $.
    $( Generalization rule for negated wff. $)
    nex $p |- -. E. x ph $=
      ( wn wex alnex mpgbi ) ADABEDBABFCG $.
      $( [18-May-94] $)
  $}

  ${
    nexd.1 $e |- ( ph -> A. x ph ) $.
    nexd.2 $e |- ( ph -> -. ps ) $.
    $( Deduction for generalization rule for negated wff. $)
    nexd $p |- ( ph -> -. E. x ps ) $=
      ( wn wal wex 19.21ai alnex sylib ) ABFZCGBCHFALCDEIBCJK $.
      $( [2-Jan-02] $)
  $}

  ${
    hbim1.1 $e |- ( ph -> A. x ph ) $.
    hbim1.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( A closed form of ~ hbim . $)
    hbim1 $p |- ( ( ph -> ps ) -> A. x ( ph -> ps ) ) $=
      ( wi wal a2i 19.21 sylibr ) ABFZABCGZFKCGABLEHABCDIJ $.
      $( [5-Aug-93] $)
  $}

  ${
    biald.1 $e |- ( ph -> A. x ph ) $.
    biald.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for universal quantifier (deduction rule). $)
    biald $p |- ( ph -> ( A. x ps <-> A. x ch ) ) $=
      ( wb wal 19.21ai 19.15 syl ) ABCGZDHBDHCDHGALDEFIBCDJK $.
      $( [5-Aug-93] $)
  $}

  ${
    biexd.1 $e |- ( ph -> A. x ph ) $.
    biexd.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for existential quantifier (deduction rule). $)
    biexd $p |- ( ph -> ( E. x ps <-> E. x ch ) ) $=
      ( wb wal wex 19.21ai 19.18 syl ) ABCGZDHBDICDIGAMDEFJBCDKL $.
      $( [5-Aug-93] $)
  $}

  ${
    exan.1 $e |- ( E. x ph /\ ps ) $.
    $( Place a conjunct in the scope of an existential quantifier. $)
    exan $p |- E. x ( ph /\ ps ) $=
      ( wa wex wal hbe1 19.27 ancom mpbi mpgbi 19.29 ax-mp exancom ) BAECFZABEC
      FBCGACFZEZPBQEZRCBQCACHIQBESDQBJKLBACMNBACOK $.
      $( [18-Aug-93] $)
  $}

  $( Split biconditional and distribute quantifier. $)
  albi $p |- ( A. x ( ph <-> ps ) <->
             ( A. x ( ph -> ps ) /\ A. x ( ps -> ph ) ) ) $=
    ( wb wal wi wa bi bial 19.26 bitr ) ABDZCEABFZBAFZGZCEMCENCEGLOCABHIMNCJK
    $.
    $( [18-Aug-93] $)

  ${
    hbnd.1 $e |- ( ph -> A. x ph ) $.
    hbnd.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( A deduction form of bound-variable hypothesis builder ~ hbne . $)
    hbnd $p |- ( ph -> ( -. ps -> A. x -. ps ) ) $=
      ( wal wi wn 19.21ai hbnt syl ) ABBCFGZCFBHZMCFGALCDEIBCJK $.
      $( [3-Jan-02] $)
  $}

  ${
    hbimd.1 $e |- ( ph -> A. x ph ) $.
    hbimd.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hbimd.3 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hbim . $)
    hbimd $p |- ( ph -> ( ( ps -> ch ) -> A. x ( ps -> ch ) ) ) $=
      ( wi wal wn hbnd com12 pm2.21 19.20i syl6 ax-1 ja ) BCHZARDIZBCASHBJZATDI
      ZSATUAABDEFKLTRDBCMNOCACDIZSACUBGLCRDCBPNOQL $.
      $( [1-Jan-02] $)
  $}

  ${
    hband.1 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hband.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hban . $)
    hband $p |- ( ph -> ( ( ps /\ ch ) -> A. x ( ps /\ ch ) ) ) $=
      ( wa wal im2and 19.26 syl6ibr ) ABCGZBDHZCDHZGLDHABMCNEFIBCDJK $.
      $( [2-Jan-02] $)
  $}

  ${
    hbbid.1 $e |- ( ph -> A. x ph ) $.
    hbbid.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hbbid.3 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hbbi . $)
    hbbid $p |- ( ph -> ( ( ps <-> ch ) -> A. x ( ps <-> ch ) ) ) $=
      ( wi wa wal wb hbimd im2and bi albi 3imtr4g ) ABCHZCBHZIQDJZRDJZIBCKZUADJ
      AQSRTABCDEFGLACBDEGFLMBCNBCDOP $.
      $( [1-Jan-02] $)
  $}

  ${
    hbald.1 $e |- ( ph -> A. y ph ) $.
    hbald.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hbal . $)
    hbald $p |- ( ph -> ( A. y ps -> A. x A. y ps ) ) $=
      ( wal 19.20d ax-7 syl6 ) ABDGZBCGZDGKCGABLDEFHBDCIJ $.
      $( [2-Jan-02] $)
  $}

  ${
    hbexd.1 $e |- ( ph -> A. y ph ) $.
    hbexd.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hbex . $)
    hbexd $p |- ( ph -> ( E. y ps -> A. x E. y ps ) ) $=
      ( wex wal 19.22d 19.12 syl6 ) ABDGZBCHZDGLCHABMDEFIBDCJK $.
      $( [2-Jan-02] $)
  $}

  $( Closed form of Theorem 19.21 of [Margaris] p. 90. $)
  19.21g $p |- ( A. x ( ph -> A. x ph ) ->
               ( A. x ( ph -> ps ) <-> ( ph -> A. x ps ) ) ) $=
    ( wal wi 19.20 syl3d com12 a4s hba1 ax-4 a1i hbimd syl3 19.20i syl6 impbid
    ) AACDZEZCDZABEZCDZABCDZEZSUBUDECUBSUDUBRUCAABCFGHITUDUDCDUBTAUCCSCJSCKUCUC
    CDETBCJLMUDUACUCBABCKNOPQ $.
    $( [27-May-97] $)

  $( Introduce a conjunct in the scope of an existential quantifier. $)
  exintr $p |- ( A. x ( ph -> ps ) -> ( E. x ph -> E. x ( ph /\ ps ) ) ) $=
    ( wi wal wa hba1 ancl a4s 19.22d ) ABDZCEAABFZCKCGKALDCABHIJ $.
    $( [11-Aug-93] $)

  ${
    aaan.1 $e |- ( ph -> A. y ph ) $.
    aaan.2 $e |- ( ps -> A. x ps ) $.
    $( Rearrange universal quantifiers. $)
    aaan $p |- ( A. x A. y ( ph /\ ps ) <-> ( A. x ph /\ A. y ps ) ) $=
      ( wa wal 19.28 bial hbal 19.27 bitr ) ABGDHZCHABDHZGZCHACHOGNPCABDEIJAOCB
      CDFKLM $.
      $( [12-Aug-93] $)
  $}

  ${
    eeor.1 $e |- ( ph -> A. y ph ) $.
    eeor.2 $e |- ( ps -> A. x ps ) $.
    $( Rearrange existential quantifiers. $)
    eeor $p |- ( E. x E. y ( ph \/ ps ) <-> ( E. x ph \/ E. y ps ) ) $=
      ( wo wex 19.45 biex hbex 19.44 bitr ) ABGDHZCHABDHZGZCHACHOGNPCABDEIJAOCB
      CDFKLM $.
      $( [8-Aug-94] $)
  $}

  $( Quantified "excluded middle".  Exercise 9.2a of Boolos, p. 111,
     _Computability and Logic_. $)
  qexmid $p |- E. x ( ph -> A. x ph ) $=
    ( wal 19.8a 19.35ri ) AABCZBFBDE $.
    $( [10-Dec-00] $)

