$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        More substitution theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$( The theorems in this section make use of the $d statement. $)

  ${
    $d x y $.
    $( ` x ` is not free in ` ( ph x | y ) ` when ` x ` and ` y ` are
       distinct. $)
    hbsbv   $p |- ( ( ph x | y ) -> A. x ( ph x | y ) ) $=
      ( weq wal wsb wi ax-16 hbsb pm2.61i ) BCDBEABCFZKBEGKBCHABCIJ $.
      $([5-Aug-93]$)
  $}

  ${
    $d x z $.
    hbsbag.1 $e |- ( ph -> A. x ph ) $.
    $( If ` x ` is not free in ` ph `, it is not free in ` ( ph y | z ) `
       when ` x ` and ` z ` are distinct. $)
    hbsbag   $p |- ( ( ph y | z ) -> A. x ( ph y | z ) ) $=
      ( weq wal wsb wi ax-16 hbsbc pm2.61i ) BDFBGACDHZMBGIMBDJACDBEKL $.
      $([12-Aug-93]$)
  $}

  ${
    $d x y $.
    $( Equivalence for substitution.  Similar to Theorem 6.1 of Quine,
       p. 40. $)
    sb5   $p |- ( ( ph x | y ) <-> E. x ( x = y /\ ph ) ) $=
      ( weq wa wex wsb hbsbv sbequ12 eqsex bicom ) BCDAEBFABCGZALBCABCHABCIJK
      $.
      $([18-Aug-93]$)
  $}

  ${
    $d x y $.
    $( Equivalence for substitution.  Compare Theorem 6.2 of Quine, p. 40. $)
    sb6   $p |- ( ( ph x | y ) <-> A. x ( x = y -> ph ) ) $=
      ( weq wi wal wsb hbsbv sbequ12 eqsal bicom ) BCDAEBFABCGZALBCABCHABCIJK
      $.
      $([18-Aug-93]$)
  $}

  ${
    $d x y $.
    $( Equivalence for substitution.  Compare Theorem 6.2 of Quine, p. 40. $)
    sb6a $p |- ( ( ph x | y ) <-> A. x ( x = y -> ( ph y | x ) ) ) $=
      ( wsb weq wi wal sb6 wb sbequ12 eqcoms pm5.74i bial bitr ) ABCDBCEZAFZBGO
      ACBDZFZBGABCHPRBOAQAQICBACBJKLMN $.
      $([5-Aug-93]$)
  $}

  ${
    $d x z $.  $d x w $.  $d y z $.
    $( A composition law for substitution.  Used in proof of Theorem 9.7 of
       Megill, p. 449. $)
    sbcocv $p |- ( ( ( ph x | y ) z | w ) <-> ( ( ph z | w ) x | y ) ) $=
      ( weq wal wsb wb wn wa wi 19.5 bi2.04 bial 19.21v bitr 3bitr3 a1i sb4b
      bilimd bialdv sylan9bbr sylan9bb 3bitr4d exp eq5 sbequ12 a4s bisbd
      bitr3d pm2.61ii ) BCFZBGZDEFZDGZABCHZDEHZADEHZBCHZIZUNJZUPJZVAVBVCKZUOUMA
      LZBGZLZDGZUMUOALZDGZLZBGZURUTVHVLIVDUMVILZBGZDGVMDGZBGVHVLVMDBMVNVGDVNUOV
      ELZBGVGVMVPBUMUOANOUOVEBPQOVOVKBUMVIDPORSVCURUOUQLZDGVBVHUQDETVBVQVGDVBUQ
      VFUOABCTUAUBUCVBUTUMUSLZBGVCVLUSBCTVCVRVKBVCUSVJUMADETUAUBUDUEUFUNUSURUTU
      NAUQDEBCDUGUMAUQIBABCUHUIUJUMUSUTIBUSBCUHUIUKUPUQURUTUOUQURIDUQDEUHUIUPAU
      SBCDEBUGUOAUSIDADEUHUIUJUKUL $.
      $([27-May-97]$)
  $}

  ${
    $d x ph $.
    $( A composition law for substitution.  Used in proof of Theorem 9.7 of
       Megill, p. 449. $)
    sbco0v $p |- ( ph <-> ( ( ph y | x ) x | y ) ) $=
      ( ax-17 sbco0 ) ABCABDE $.
      $([5-Aug-93]$)
  $}

  ${
    $d x y $.  $d x ph $.
    $( Elimination of substitution. $)
    sbelx $p |- ( ph <-> E. x ( x = y /\ ( ph y | x ) ) ) $=
      ( wsb weq wa wex sbco0v sb5 bitr ) AACBDZBCDBCEKFBGABCHKBCIJ $.
      $([5-Aug-93]$)
  $}

  ${
    $( Note:  A more general case could also be proved with
       "$d x z $.  $d y w $.  $d x ph $.  $d y ph $.", but with more
       difficulty. $)
    $d x y z w $.  $d x y ph $.
    $( Elimination of double substitution. $)
    sbel2x $p |- ( ph <-> E. x E. y ( ( x = z /\ y = w ) /\
                     ( ( ph z | x ) w | y ) ) ) $=
      ( weq wsb wa wex sbelx bilan biex exdistr 3bitr4 anass bi2ex bitr4 ) ABDF
      ZCEFZADBGZECGZHZHZCIBIZRSHUAHZCIBIRTHZBIRUBCIZHZBIAUDUFUHBTUGRTCEJKLABDJR
      UBBCMNUEUCBCRSUAOPQ $.
      $([5-Aug-93]$)
  $}


  ${
    $d x y $.
    $( A theorem used in elimination of disjoint variable restriction on ` x `
       and ` y ` by replacing it with a distinctor ` -. A. x x = z `. $)
    sbadd $p |- ( -. A. x x = z ->
             ( ( A. x ph y | z ) <-> A. x ( ph y | z ) ) ) $=
      ( weq wal wn wsb wb wi sbequ12 a4s 19.20i eq5s 19.15 syl bitr3d a1d wa
      sb4 ax-16 ax-12 pm2.61i hba1 a1i hbimd eq6s ax-4 syl3 syl6 19.20ii sb2
      a7s sylan9 ax-7 19.20 syl9 impbid exp ) CDEZCFZBDEBFGZABFZCDHZACDHZBFZIZJ
      VAVGVBVAVCVDVFUTVCVDICVCCDKLVAAVEIZBFZVCVFIVICDBVAVHBUTVHCACDKLMNAVEBOPQR
      VAGZVBVGVJVBSVDVFVJVDUTVCJZCFZVBVFVCCDTVBVLUTAJZBFZCFZVFVLVOJBDCVBVKVNCVB
      VKVKBFZVNVKVPJBDBVBUTVCBBCEBFZVBUTUTBFZJZJVQVSVBUTBCUARCDBUBUCZVCVCBFJVBA
      BUDUEUFUGVKVMBVCAUTABUHUIMUJUKUGVMVFBCVMCFZVEBACDULMUMUJUNVJVFVOVBVDVJVFW
      ABFZVOVFWBJCDBVJVEWABACDTUKUGVMBCUOUJVOVDJBDCVBCFVOVLVDVBVNVKCVBUTVRVNVCV
      TUTABUPUQUKVCCDULUJUGUNURUSUC $.
      $([5-Aug-93]$)
  $}

  ${
    $d x y $.  $d x z $.
    $( Removal of quantifier from substitution. $)
    sbal $p |- ( ( A. x ph y | z ) <-> A. x ( ph y | z ) ) $=
      ( weq wal wsb wb sbequ5 a16gb sbim sylbi sbb sylib bitr3d sbadd pm2.61i
      ) BDEBFZABFZCDGZACDGZBFZHRUATUBRASHZCDGZUATHRRCDGUDBDCDIRUCCDABDBJKLASCDM
      NUABDBJOABCDPQ $.
      $([5-Aug-93]$)
  $}

  ${
    $d x z $.  $d y z $.
    sbalv.1 $e |- ( ( ph x | y ) <-> ps ) $.
    $( Quantify with new variable inside substitution. $)
    sbalv   $p |- ( ( A. z ph x | y ) <-> A. z ps ) $=
      ( wal wsb sbal bial bitr ) AEGCDHACDHZEGBEGAECDILBEFJK $.
      $([18-Aug-93]$)
  $}

  ${
    $d x z $.  $d y z $.  $d z ps $.
    ddelim.1 $e |- ( z = y -> ( ph <-> ps ) ) $.
    ddelim.2 $e |- ( ph -> A. x ph ) $.
    $( Theorem to eliminate a distinct variable restriction on ` x ` and
       ` z ` and replace it with a distinctor as an antecedent.  ` ph ` is
       considered to contain ` z ` and ` ps ` to have ` z ` replaced with
       ` y `, and we don't require that ` x ` and ` y ` be distinct. $)
    ddelim   $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( weq wal wn wi wsb wb ax-17 ddel4 a1i sbadd bial syl6bb bi2imd sbi
      syl5bb stdpc4 mpg mpbii ) CDHCIJZAACIZKZEDLZBBCIZKZUFAEDLZUGEDLZKUKUIUFUL
      BUMUJULBMUFABEDBENFOZPUFUMULCIUJACEDQULBCUNRSTAUGEDUAUBUHUIEUHEDUCGUDUE
      $.
      $([23-Nov-94]$)
  $}

  ${
    $d w v y $.  $d w v z $.  $d w x $.  $( ` w v ` are dummy. $)
    $( Axiom ~ax-15 is redundant if we assume ~ax-17.  Remark 9.6 in Megill,
       p. 448, regarding axiom scheme C14'. $)
    ax15 $p |- ( -. A. z z = x -> ( -. A. z z = y ->
              ( x e. y -> A. z x e. y ) ) ) $=
      ( vw vv weq wal wn wel wi a13b bilimd hbn1 a14b ax-17 ddelim hbim1 19.21
      syl6ib pm2.86d ) CAFCGHZCBFZCGHZABIZUDCGZUAUCUDJZUFCGUCUEJUCDBIZJUFCADDAF
      UGUDUCDABKLUCUGCUBCMZDEIZUGCBEEBDNUICOPQPUCUDCUHRST $.
      $([29-Jun-95]$)
  $}

