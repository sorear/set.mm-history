$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Existential uniqueness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbols needed for uniqueness notation. $)
  $c E! $.  $( Backwards E exclamation point. $)
  $c E* $.  $( Backwards E superscript *. $)

  $( Extend wff definition to include existential uniqueness ('there exists a
     unique ` x ` such that...'). $)
  weu $a wff E! x ph $.

  $( Extend wff definition to include uniqueness ('there exists at most one
     ` x ` such that...'). $)
  wmo $a wff E* x ph $.

  ${
    $d x y $.  $d y ph $.
    $( Define existential uniqueness, i.e. "there exists exactly one."
       Definition 10.1 of Bell and Machover, p. 97. $)
    df-eu $a |- ( E! x ph <-> E. y A. x ( ph <-> x = y ) ) $.
  $}

  $( Define "there exists at most one".   Compare definition of Bell and
     Machover, p. 460. $)
  df-mo $a |- ( E* x ph <-> ( E. x ph -> E! x ph ) ) $.

  ${
    $d x y z $.  $d ph z $.  $( ` z ` is dummy. $)
    euf.1 $e |- ( ph -> A. y ph ) $.
    $( A stronger version of the existential uniqueness definition. $)
    euf   $p |- ( E! x ph <-> E. y A. x ( ph <-> x = y ) ) $=
      ( vz weu weq wb wal wex df-eu ax-17 hbbi hbal eqt2b bilbid bialdv cbvex
      bitr ) ABFABEGZHZBIZEJABCGZHZBIZCJABEKUBUEECUACBATCDTCLMNUEELECGZUAUDBUFT
      UCAECBOPQRS $.
      $([12-Aug-93]$)
  $}

  ${
    $d x y $.  $d y ph $.  $d y ps $.  $d y ch $.  $( ` y ` is dummy. $)
    bieud.1 $e |- ( ph -> A. x ph ) $.
    bieud.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for uniqueness quantifier (deduction rule). $)
    bieud   $p |- ( ph -> ( E! x ps <-> E! x ch ) ) $=
      ( vy weq wb wal wex weu birbid biald biexdv df-eu 3bitr4g ) ABDGHZIZDJZGK
      CRIZDJZGKBDLCDLATUBGASUADEABCRFMNOBDGPCDGPQ $.
      $([9-Jul-94]$)
  $}

  ${
    $d x ph $.
    bieudv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for uniqueness quantifier (deduction rule). $)
    bieudv   $p |- ( ph -> ( E! x ps <-> E! x ch ) ) $=
      ( ax-17 bieud ) ABCDADFEG $.
      $([9-Jul-94]$)
  $}

  ${
    bieu.1 $e |- ( ph <-> ps ) $.
    $( Introduce uniqueness quantifier to both sides of an equivalence. $)
    bieu   $p |- ( E! x ph <-> E! x ps ) $=
      ( weq weu wb eqid hbth a1i bieud ax-mp ) CCEZACFBCFGCHZMABCMCNIABGMDJKL
      $.
      $([9-Jul-94]$)
  $}

  ${
    $d x y $.  $d y ph $.  $( ` y ` is dummy. $)
    $( Free-variable hypothesis builder for uniqueness. $)
    hbeu1 $p |- ( E! x ph -> A. x E! x ph ) $=
      ( vy weq wb wal wex weu hba1 hbex df-eu bial 3imtr4 ) ABCDEZBFZCGZPBFABHZ
      QBFOBCNBIJABCKZQPBRLM $.
      $([9-Jul-94]$)
  $}

  ${
    $d y z w $.  $d x z w $.  $d z ph $.  $( ` z w ` are dummy. $)
    hbeu.1 $e |- ( ph -> A. x ph ) $.
    $( Free-variable hypothesis builder for "at most one".  Note that ` x ` and
       ` y ` needn't be distinct (this makes the proof more difficult). $)
    hbeu   $p |- ( E! y ph -> A. x E! y ph ) $=
      ( vz vw weq wb wal wex weu wi ax-10 eq4s hba1 syl5 wn eq6 a1i a8b ax17eq
      ddelim hbbid eq6s 19.21ai 19.20 syl ax-7 syl6 pm2.61i hbex df-eu bial
      3imtr4 ) ACEGZHZCIZEJZURBIACKZUSBIUQBEBCGBIZUQUQBIZLUTUQCIZVAUQVBVALCBUQC
      BMNUPCOPUTQZUQUPBIZCIZVAVCUPVDLZCIUQVELVCVFCBCCRVFBCBVCAUOBAABILVCDSFEGUO
      BCFFCETFEBUAUBUCUDUEUPVDCUFUGUPCBUHUIUJUKACEULZUSURBVGUMUN $.
      $([8-Mar-95]$)
  $}

  ${
    $d x y z $.  $d ph z $. $( ` z ` is dummy. $)
    sb8eu.1 $e |- ( ph -> A. y ph ) $.
    $( Variable substitution in uniqueness quantifier.  (This theorem can also
       be proved without requiring that ` x ` and ` y ` be distinct, but
       the proof would be longer.) $)
    sb8eu   $p |- ( E! x ph <-> E! y ( ph x | y ) ) $=
      ( vz weq wb wal wex wsb weu ax17eq hbbi sb8 a8b ddel4 sblbis bial bitr
      biex df-eu 3bitr4 ) ABEFZGZBHZEIABCJZCEFZGZCHZEIABKUFCKUEUIEUEUDBCJZCHUIU
      DBCAUCCDBECLMNUJUHCUCUGABCUCUGBCCEBLBCEOPQRSTABEUAUFCEUAUB $.
      $([7-Aug-94]$)
  $}

  ${
    $d x y $.
    cbveu.1 $e |- ( ph -> A. y ph ) $.
    cbveu.2 $e |- ( ps -> A. x ps ) $.
    cbveu.3 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbveu   $p |- ( E! x ph <-> E! y ps ) $=
      ( weu wsb sb8eu ddel4 bieu bitr ) ACHACDIZDHBDHACDEJNBDABCDFGKLM $.
      $([25-Nov-94]$)
  $}

  ${
    $d x y $.
    eu1.1 $e |- ( ph -> A. y ph ) $.
    $( An alternate way of expressing uniqueness used by some authors. $)
    eu1   $p |- ( E! x ph <->
                E. x ( ph /\ A. y ( ( ph x | y ) -> x = y ) ) ) $=
      ( wsb weu weq wb wal wex wi wa hbsbv euf sb8eu eqcomb bilim bial sb5f1
      bi2an ancom albi 3bitr4 biex ) ABCEZCFUECBGZHCIZBJABFAUEBCGZKZCIZLZBJUECB
      ABCMNABCDOUKUGBUJALUEUFKZCIZUFUEKCIZLUKUGUJUMAUNUIULCUHUFUEBCPQRACBDSTAUJ
      UAUEUFCUBUCUDUC $.
      $([20-Aug-93]$)
  $}

  ${
    $d x y z $.  $d ph z $.  $( ` z ` is dummy. $)
    mo.1 $e |- ( ph -> A. y ph ) $.
    $( Equivalent definitions of "there exists at most one". $)
    mo   $p |- ( E. y A. x ( ph -> x = y ) <->
               A. x A. y ( ( ph /\ ( ph x | y ) ) -> x = y ) ) $=
      ( vz weq wi wal wex wsb wa ax17eq hbim hbal ax-17 eqt2b bilimd bialdv
      cbvex hbsbv sbequ2 ax-8 syl34d cbv3 ancli aaan sylibr prth eqan syl6
      19.20i syl 19.23aiv sylbir hbsbb 19.22i 19.20 a7s 19.22 comm impexp
      bi2.04 bitr bi2al syl5ib wn 19.7 hbne sbequ1 eqcoms pm2.16d pm2.21 19.8a
      3syl a1d pm2.61i impbi ) ABCFZGZBHZCIZAABCJZKZVRGZCHZBHZWAABEFZGZBHZEIWFW
      IVTECWHCBAWGCDBECLMZNVTEOECFZWHVSBWKWGVRAECBPQRSWIWFEWIWHWBCEFZGZKZCHZBHZ
      WFWIWIWMCHZKWPWIWQWHWMBCWJWBWLBABCTCEBLMZVRWBAWGWLABCUABCEUBUCUDUEWHWMBCW
      JWRUFUGWOWEBWNWDCWNWCWGWLKVRAWGWBWLUHBCEUIUJUKUKULUMUNWBCIZWFWAGWSWBVSGZC
      HBHZWAWFWSWBBHZCIZXAWAGWBXBCABCDUOZUPXAXCWAXAXBVTGZCHZXCWAGWTXFCBWTBHXECW
      BVSBUQUKURXBVTCUSULUTULWDWTBCWDAWBVRGGWTAWBVRVAAWBVRVBVCVDVEWSVFZWAWFXGWB
      VFZCHZWAWBCVGXIAVFZBHVTWAXHXJCBWBBXDVHACDVHCBFAWBAWBGBCABCVIVJVKUDXJVSBAV
      RVLUKVTCVMVNUNVOVPVQ $.
      $([7-Aug-94]$)
  $}

  ${
    $d x y $.  $d ph y $.  $( ` y ` is dummy. $)
    $( Existential uniqueness implies existence. $)
    euex $p |- ( E! x ph -> E. x ph ) $=
      ( vy weu wex wsb weq wi wal wa ax-17 eu1 19.40 sylbi pm3.26d ) ABDZABEZAB
      CFBCGHCIZBEZPARJBEQSJABCACKLARBMNO $.
      $([15-Sep-93]$)
  $}

  ${
    $d x y $.
    eumo0.1 $e |- ( ph -> A. y ph ) $.
    $( Existential uniqueness implies "at most one". $)
    eumo0   $p |- ( E! x ph -> E. y A. x ( ph -> x = y ) ) $=
      ( weu weq wb wal wex wi euf bi1 19.20i 19.22i sylbi ) ABEABCFZGZBHZCIAPJZ
      BHZCIABCDKRTCQSBAPLMNO $.
      $([8-Jul-94]$)
  $}

  ${
    $d x y $.
    eu2.1 $e |- ( ph -> A. y ph ) $.
    $( An alternate way of expressing uniqueness used by some authors. $)
    eu2   $p |- ( E! x ph <->
             ( E. x ph /\ A. x A. y ( ( ph /\ ( ph x | y ) ) -> x = y ) ) )
             $=
      ( weu wex wsb wa weq wi wal euex eumo0 mo sylib jca 19.29r impexp bial
      19.21 bitr bilan abai bitr4 biex eu1 sylibr impbi ) ABEZABFZAABCGZHBCIZJZ
      CKZBKZHZUIUJUOABLUIAULJBKCFUOABCDMABCDNOPUPAUKULJZCKZHZBFZUIUPAUNHZBFUTAU
      NBQVAUSBVAAAURJZHUSUNVBAUNAUQJZCKVBUMVCCAUKULRSAUQCDTUAUBAURUCUDUEOABCDUF
      UGUH $.
      $([8-Jul-94]$)
  $}

  ${
    $d x y $.
    eu3.1 $e |- ( ph -> A. y ph ) $.
    $( An alternate way of expressing uniqueness. $)
    eu3   $p |- ( E! x ph <->
                ( E. x ph /\ E. y A. x ( ph -> x = y ) ) ) $=
      ( weu wex wsb wa weq wi wal eu2 mo bilan bitr4 ) ABEABFZAABCGHBCIZJCKBKZH
      PAQJBKCFZHABCDLSRPABCDMNO $.
      $([8-Jul-94]$)
  $}

  ${
    $d x y ph $.  $d y ps $.  $( ` y ` is dummy. $)
    $( Introduction of a disjunct into uniqueness quantifier. $)
    euorv $p |- ( ( -. ph /\ E! x ps ) -> E! x ( ph \/ ps ) ) $=
      ( vy wn weu wo weq wb wal wex biorf birbid biimpd 19.20dv 19.22dv df-eu
      3imtr4g imp ) AEZBCFZABGZCFZTBCDHZIZCJZDKUBUDIZCJZDKUAUCTUFUHDTUEUGCTUEUG
      TBUBUDABLMNOPBCDQUBCDQRS $.
      $([23-Mar-95]$)
  $}

  ${
    $d x y $.
    mo2.1 $e |- ( ph -> A. y ph ) $.
    $( Alternate definition of "at most one". $)
    mo2   $p |- ( E* x ph <-> E. y A. x ( ph -> x = y ) ) $=
      ( wmo wex weu wi weq wal df-mo wn 19.7 pm2.21 19.20i 19.8a syl sylbir
      eumo0 ja wa eu3 biimpr exp comm impbi bitr ) ABEABFZABGZHZABCIZHZBJZCFZAB
      KUJUNUHUIUNUHLALZBJZUNABMUPUMUNUOULBAUKNOUMCPQRABCDSTUHUNUIUHUNUIUIUHUNUA
      ABCDUBUCUDUEUFUG $.
      $([8-Mar-95]$)
  $}

  ${
    $d x y $.
    mo3.1 $e |- ( ph -> A. y ph ) $.
    $( Alternate definition of "at most one". $)
    mo3   $p |- ( E* x ph <->
               A. x A. y ( ( ph /\ ( ph x | y ) ) -> x = y ) ) $=
      ( wmo weq wi wal wex wsb wa mo2 mo bitr ) ABEABCFZGBHCIAABCJKOGCHBHABCDLA
      BCDMN $.
      $([8-Mar-95]$)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    mo4.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( "At most one" using implicit substitution. $)
    mo4 $p |- ( E* x ph <-> A. x A. y ( ( ph /\ ps ) -> x = y ) ) $=
      ( wmo wsb wa weq wi wal ax-17 mo3 ddel4 bilan birim bi2al bitr ) ACFAACDG
      ZHZCDIZJZDKCKABHZUAJZDKCKACDADLMUBUDCDTUCUASBAABCDBCLENOPQR $.
      $([26-Jul-95]$)
  $}

  ${
    bimod.1 $e |- ( ph -> A. x ph ) $.
    bimod.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for "at most one" quantifier (deduction rule). $)
    bimod   $p |- ( ph -> ( E* x ps <-> E* x ch ) ) $=
      ( wex weu wi wmo biexd bieud bi2imd df-mo 3bitr4g ) ABDGZBDHZICDGZCDHZIBD
      JCDJAPRQSABCDEFKABCDEFLMBDNCDNO $.
      $([8-Mar-95]$)
  $}

  ${
    bimo.1 $e |- ( ps <-> ch ) $.
    $( Formula-building rule for "at most one" quantifier (inference rule). $)
    bimo   $p |- ( E* x ps <-> E* x ch ) $=
      ( weq wmo wb eqid hbth a1i bimod ax-mp ) CCEZACFBCFGCHZMABCMCNIABGMDJKL
      $.
      $([9-Mar-95]$)
  $}

  $( Free-variable hypothesis builder for "at most one". $)
  hbmo1 $p |- ( E* x ph -> A. x E* x ph ) $=
    ( wex weu wi wal wmo hbe1 hbeu1 hbim df-mo bial 3imtr4 ) ABCZABDZEZPBFABGZQ
    BFNOBABHABIJABKZQPBRLM $.
    $([8-Mar-95]$)

  ${
    hbmo.1 $e |- ( ph -> A. x ph ) $.
    $( Free-variable hypothesis builder for "at most one". $)
    hbmo $p |- ( E* y ph -> A. x E* y ph ) $=
      ( wex weu wi wal wmo hbex hbeu hbim df-mo bial 3imtr4 ) ACEZACFZGZRBHACIZ
      SBHPQBABCDJABCDKLACMZSRBTNO $.
      $([9-Mar-95]$)
  $}

  ${
    $d x y $.
    cbvmo.1 $e |- ( ph -> A. y ph ) $.
    cbvmo.2 $e |- ( ps -> A. x ps ) $.
    cbvmo.3 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvmo   $p |- ( E* x ph <-> E* y ps ) $=
      ( wex weu wi wmo cbvex cbveu bi2im df-mo 3bitr4 ) ACHZACIZJBDHZBDIZJACKBD
      KQSRTABCDEFGLABCDEFGMNACOBDOP $.
      $([9-Mar-95]$)
  $}

  ${
    $d x y $.  $d y ph $.  $( ` y ` is dummy. $)
    $( Uniqueness in terms of "at most one". $)
    eu5 $p |- ( E! x ph <-> ( E. x ph /\ E* x ph ) ) $=
      ( vy weu wex weq wi wal wa wmo ax-17 eu3 mo2 bilan bitr4 ) ABDABEZABCFGBH
      CEZIPABJZIABCACKZLRQPABCSMNO $.
      $([23-Mar-95]$)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    eu4.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Uniqueness using implicit substitution. $)
    eu4 $p |- ( E! x ph <-> ( E. x ph /\
             A. x A. y ( ( ph /\ ps ) -> x = y ) ) ) $=
      ( weu wex wmo wa weq wi wal eu5 mo4 bilan bitr ) ACFACGZACHZIQABICDJKDLCL
      ZIACMRSQABCDENOP $.
      $([26-Jul-95]$)
  $}

  $( Existential uniqueness implies "at most one". $)
  eumo $p |- ( E! x ph -> E* x ph ) $=
    ( weu wex wmo eu5 pm3.27bd ) ABCABDABEABFG $.
    $([23-Mar-95]$)

  ${
    eumoi.1 $e |- E! x ph $.
    $( "At most one" inferred from existential uniqueness. $)
    eumoi   $p |- E* x ph $=
      ( weu wmo eumo ax-mp ) ABDABECABFG $.
      $([5-Apr-95]$)
  $}

  $( Something exists or at most one exists. $)
  exmo $p |- ( E. x ph \/ E* x ph ) $=
    ( wex wmo wn weu wi pm2.21 df-mo sylibr orri ) ABCZABDZLELABFZGMLNHABIJK $.
    $([8-Mar-95]$)

  ${
    $d x y $.  $d y ph $.  $d y ps $.  $( ` y ` is dummy. $)
    $( "At most one" is preserved through implication (notice wff reversal). $)
    immo   $p |- ( A. x ( ph -> ps ) -> ( E* x ps -> E* x ph ) ) $=
      ( vy wi wal weq wex wmo syl2 19.20ii 19.22dv ax-17 mo2 3imtr4g ) ABEZCFZB
      CDGZEZCFZDHAREZCFZDHBCIACIQTUBDPSUACABRJKLBCDBDMNACDADMNO $.
      $([22-Apr-95]$)
  $}

  ${
    $d x y $.  $d x y ph $.  $d y ps $.  $( ` y ` is dummy. $)
    $( Move antecedent outside of "at most one". $)
    moimv   $p |- ( E* x ( ph -> ps ) -> ( ph -> E* x ps ) ) $=
      ( vy wi wmo weq wal wex ax-1 a1i syl4d 19.20dv 19.22dv ax-17 mo2 3imtr4g
      comm ) AABEZCFZBCFZASCDGZEZCHZDIBUBEZCHZDITUAAUDUFDAUCUECABSUBBSEABAJKLMN
      SCDSDOPBCDBDOPQR $.
      $([28-Jul-95]$)
  $}

  $( Uniqueness implies "at most one" through implication. $)
  euimmo   $p |- ( A. x ( ph -> ps ) -> ( E! x ps -> E* x ph ) ) $=
    ( wi wal wmo weu immo eumo syl5 ) ABDCEBCFACFBCGABCHBCIJ $.
    $([22-Apr-95]$)

  $( "At most one" is still the case when a conjunct is added. $)
  moan   $p |- ( E* x ph -> E* x ( ps /\ ph ) ) $=
    ( wa wi wmo immo pm3.27 mpg ) BADZAEACFJCFECJACGBAHI $.
    $([22-Apr-95]$)

  ${
    moani.1 $e |- E* x ph $.
    $("At most one" is still true when a conjunct is added. $)
    moani   $p |- E* x ( ps /\ ph ) $=
      ( wmo wa moan ax-mp ) ACEBAFCEDABCGH $.
      $([9-Mar-95]$)
  $}

  ${
    $d x y $.  $d y ph $.  $d y ps $.  $( ` y ` is dummy. $)
    moanim.1 $e |- ( ph -> A. x ph ) $.
    $( Introduction of a conjunct into "at most one" quantifier. $)
    moanim $p |- ( E* x ( ph /\ ps ) <-> ( ph -> E* x ps ) ) $=
      ( vy wa weq wi wal wex wmo impexp bial 19.21 bitr biex ax-17 mo2 bilim
      19.37v bitr4 3bitr4 ) ABFZCEGZHZCIZEJABUDHZCIZHZEJZUCCKABCKZHZUFUIEUFAUGH
      ZCIUIUEUMCABUDLMAUGCDNOPUCCEUCEQRULAUHEJZHUJUKUNABCEBEQRSAUHETUAUB $.
      $([3-Dec-01]$)
  $}

  ${
    $d x y ph $.  $d y ps $.  $( ` y ` is dummy. $)
    $( Introduction of a conjunct into "at most one" quantifier. $)
    moanimv $p |- ( E* x ( ph /\ ps ) <-> ( ph -> E* x ps ) ) $=
      ( ax-17 moanim ) ABCACDE $.
      $([23-Mar-95]$)
  $}

  ${
    $d x ph $.
    $( Introduction of a conjunct into uniqueness quantifier. $)
    euanv $p |- ( E! x ( ph /\ ps ) <-> ( ph /\ E! x ps ) ) $=
      ( wa wex wmo weu 19.42v wi moanimv bilan abai bitr4 bi2an anass an4 3bitr4
      eu5 anabs1 biex 19.41v bitr biran anabs ) ABDZCEZADZUECFZDZAADZBCEZBCFZDZ
      DZUECGZABCGZDUFAUHDZDAUKDZAULDZDUIUNUFURUQUSABCHUQAAULIZDUSUHUTAABCJKAULL
      MNUFAUHOAAUKULPQUOUFUHDUIUECRUFUGUHUFUEADZCEUGUEVACABSTUEACUAUBUCUBAUJUPU
      MAUDBCRNQ $.
      $([23-Mar-95]$)
  $}

  ${
    $d x y $.  $d y ph $.  $d y ps $.  $( ` y ` is dummy. $)
    $( "At most one" picks a variable value. $)
    mopick $p |- ( ( E* x ph /\ E. x ( ph /\ ps ) ) -> ( ph -> ps ) ) $=
      ( vy wmo wa wex wi wsb ax-17 hbsbv hban weq sbequ12 bi2and cbvex sbequ2
      syl3 exp3a com4t imp wal mo3 ax-4 a4s sylbi syl5 19.23aiv comm ) ACEZABFZ
      CGZABHZULUJUMULACDIZBCDIZFZDGUJUMHZUKUPCDUKDJUNUOCACDKBCDKLCDMZAUNBUOACDN
      BCDNOPUPUQDUPAUNFZURHZUMUJUNUOUTUMHUTAUNUOBUTAUNUOBHZURVAUSBCDQRSTUAUJUTD
      UBZCUBUTACDADJUCVBUTCUTDUDUEUFUGUHUFUIUA $.
      $([27-Jan-97]$)
  $}

  $( Existential uniqueness "picks" a variable value for which another wff is
     true. $)
  eupick $p |- ( ( E! x ph /\ E. x ( ph /\ ps ) ) -> ( ph -> ps ) ) $=
    ( wmo wa wex wi weu mopick eumo sylan ) ACDABECFABGACHABCIACJK $.
    $([10-Jul-94]$)

  $( Existential uniqueness "pick" showing wff equivalence. $)
  eupickb $p |- ( ( E! x ph /\ E! x ps /\ E. x ( ph /\ ps ) ) ->
               ( ph <-> ps ) ) $=
    ( weu wa wex w3a wi eupick 3adant2 3simpc ancom biimp 19.22i imlan 3syl
    impbid ) ACDZBCDZABEZCFZGZABRUAABHSABCIJUBSUAESBAEZCFZEBAHRSUAKUAUDSTUCCTUC
    ABLMNOBACIPQ $.
    $([25-Nov-94]$)

  ${
    moexex.1 $e |- ( ph -> A. y ph ) $.
    $( "At most one" double quantification. $)
    moexex $p |- ( ( E* x ph /\ A. x E* y ps ) -> E* y E. x ( ph /\ ps ) ) $=
      ( wmo wal wa wex wi hbmo1 hba1 hbe1 hbmo hbim mopick exp com3r 19.21ad
      immo a4sd syl6 19.23ai wn hbex pm3.26 19.22i con3 exmo ori syl a1d
      pm2.61i imp ) ACFZBDFZCGZABHZCIZDFZACIZUOUQUTJZJZAVCCUOVBCACKUQUTCUPCLUSC
      DURCMNOOAUOUSBJZDGZVBAUOVDDEADCENUOUSABUOUSABJABCPQRSVEUPUTCUSBDTUAUBUCVA
      UDZVBUOVFUTUQVFUSDIZUDUTVGVAUSVADADCEUEURACABUFUGUCUHVGUTUSDUIUJUKULULUMU
      N $.
      $([3-Dec-01]$)
  $}

  ${
    $d x y $.  $d y ph $.
    $( "At most one" double quantification. $)
    moexexv $p |- ( ( E* x ph /\ A. x E* y ps ) -> E* y E. x ( ph /\ ps ) ) $=
      ( ax-17 moexex ) ABCDADEF $.
      $([26-Jan-97]$)
  $}

  $( Double quantification with "at most one". $)
  2moex $p |- ( E* x E. y ph -> A. y E* x ph ) $=
    ( wex wmo hbe1 hbmo wi immo 19.8a mpg 19.21ai ) ACDZBEZABEZCMCBACFGAMHNOHBA
    MBIACJKL $.
    $([3-Dec-01]$)

  $( Double quantification with existential uniqueness. $)
  2euex $p |- ( E! x E. y ph -> E. y E! x ph ) $=
    ( wex weu wmo wa eu5 hbe1 hbmo 19.41 biimpr 19.11 sylanb wal 2moex ax-4
    syl imlan sylibr 19.22i sylbi ) ACDZBEUCBDZUCBFZGZABEZCDZUCBHUFABDZUEGZCDZU
    HUICDZUEUKUDUKULUEGUIUECUCCBACIJKLABCMNUJUGCUJUIABFZGUGUEUMUIUEUMCOUMABCPUM
    CQRSABHTUARUB $.
    $([3-Dec-01]$)

  $( Double quantification with existential uniqueness and "at most one". $)
  2eumo $p |- ( E! x E* y ph -> E* x E! y ph ) $=
    ( weu wmo wi euimmo eumo mpg ) ACDZACEZFKBDJBEFBJKBGACHI $.
    $([3-Dec-01]$)

  $( Double existential uniqueness. $)
  2eu2ex $p |- ( E! x E! y ph -> E. x E. y ph ) $=
    ( weu wex euex 19.22i syl ) ACDZBDIBEACEZBEIBFIJBACFGH $.
    $([3-Dec-01]$)

  $( Swap of "at most one" quantifier. $)
  2moswap $p |- ( ( E* x E. y ph /\ A. x E* y ph ) -> E* y E. x ph ) $=
    ( wex wmo wal wa hbe1 moexex wi wb 19.8a pm4.71r mpbi biex bimo sylibr ) AC
    DZBEACEBFGRAGZBDZCEABDZCERABCACHIUATCASBARJASKACLARMNOPQ $.
    $([3-Dec-01]$)

  $( Double existential uniqueness. $)
  2exeu $p |- ( ( E! x E. y ph /\ E! y E. x ph ) -> E! x E! y ph ) $=
    ( wex wmo wa weu hbe1 hbmo 19.41 wi immo 19.8a mpg imlan 19.22i sylbir
    19.11 sylanb pm3.26 adantl im2an ancoms eu5 bi2an biex bimo bitr 3imtr4 )
    ACDZBDZUJBEZFZABDZCDZUNCEZFZFUJACEZFZBDZUSBEZFZUJBGZUNCGZFACGZBGZUQUMVBUQUT
    UMVAUKUPUTUOUKUPFUJUPFZBDUTUJUPBUNBCABHIJVGUSBUPURUJAUNKUPURKCAUNCLABMNOPQA
    CBRSULVAUKUSUJKULVAKBUSUJBLUJURTNUAUBUCVCUMVDUQUJBUDUNCUDUEVFVEBDZVEBEZFVBV
    EBUDVHUTVIVAVEUSBACUDZUFVEUSBVJUGUEUHUI $.
    $([3-Dec-01]$)

  $( Double existential uniqueness. $)
  2eu1 $p |- ( A. x E* y ph ->
        ( E! x E! y ph <-> ( E! x E. y ph /\ E! y E. x ph ) ) ) $=
    ( wmo wal weu wex wa 2eu2ex 19.11 sylib jca a1d wi eu5 biex bimo bi2an
    bitr pm3.27bd immo ancom ax-4 imlan sylbi mpg hba1 moanim ancrd 2moswap
    exp imdistani syl6 syl jcad an4 syl6ibr comm 2exeu a1i impbid ) ACDZBEZACFZ
    BFZACGZBFZABGZCFZHZVEVCVJVEVCVFBGZVHCGZHZVFBDZVHCDZHZHZVJVEVCVMVPVEVMVCVEVK
    VLABCIZVEVKVLVRABCJKLMVEVFVBHZBDZVCVPNVEVSBGZVTVEVDBGZVDBDZHWAVTHVDBOWBWAWC
    VTVDVSBACOZPVDVSBWDQRSTVTVCVNVCHVPVTVCVNVTVCVFHZBDZVCVNNWEVSNVTWFNBWEVSBUAW
    EVFVCHVSVCVFUBVCVBVFVBBUCUDUEUFVCVFBVBBUGUHKUIVNVCVOVNVCVOABCUJUKULUMUNUOVJ
    VKVNHZVLVOHZHVQVGWGVIWHVFBOVHCORVKVNVLVOUPSUQURVJVENVCABCUSUTVA $.
    $([3-Dec-01]$)

  $( Double existential uniqueness. $)
  2eu2 $p |- ( E! y E. x ph -> ( E! x E! y ph <-> E! x E. y ph ) ) $=
    ( wex weu wmo wal wi eumo 2moex wa 2eu1 pm3.26 syl6bi 3syl 2exeu exp comm
    impbid ) ABDZCEZACEBEZACDBEZUATCFACFBGZUBUCHTCIACBJUDUBUCUAKUCABCLUCUAMNOUC
    UAUBUCUAUBABCPQRS $.
    $([3-Dec-01]$)


  $( Double existential uniqueness. $)
  2eu3 $p |- ( A. x A. y ( E* x ph \/ E* y ph ) ->
 ( ( E! x E! y ph /\ E! y E! x ph ) <-> ( E! x E. y ph /\ E! y E. x ph ) ) ) $=
    ( wmo wo wal weu wa wex wb hbmo1 19.31 bial hbal 19.32 bitr wi 2eu1 biimpd
    ancom syl6ib adantld adantrd jaoi 2exeu ancoms jca a1i impbid sylbi ) ABDZA
    CDZECFZBFZUKCFZULBFZEZACGBGZABGCGZHZACIBGZABICGZHZJUNUOULEZBFUQUMVDBUKULCAC
    KLMUOULBUKBCABKNOPUQUTVCUOUTVCQUPUOUSVCURUOUSVBVAHZVCUOUSVEACBRSVBVATUAUBUP
    URVCUSUPURVCABCRSUCUDVCUTQUQVCURUSABCUEVBVAUSACBUEUFUGUHUIUJ $.
    $([3-Dec-01]$)

$(
  ${
    $v u $.
    $d x y ps u v $.  $d z w ph u v $.
    2mo.1 $e |- ( ( x = z /\ y = w ) -> ( ph <-> ps ) ) $.
    $ ( Double "exists at most one". $ )
    2mo $p |- ( E. z E. w A. x A. y ( ph -> ( x = z /\ y = w ) ) <->
             A. x A. y A. z A. w ( ( ph /\ ps ) -> ( x = z /\ y = w ) ) ) $=
?$.
  $}
$)

  ${
    $d x y z w $.  $d z w ph $.
    $( Double existential uniqueness. $)
    2eu4 $p |- ( ( E! x E. y ph /\ E! y E. x ph ) <->
      ( E. x E. y ph /\ E. z E. w A. x A. y ( ph -> ( x = z /\ y = w ) ) ) ) $=
      ( wex weu wa weq wi wal ax-17 eu3 bi2an an4 19.11 bilan anabs bitr4 hba1
      ax-4 impbi jcab bial 19.26 bitr 3bitr4 19.5 19.23v bi2al hbe1 ax17eq
      hbim aaan 3bitr bi2ex eeanv bitr2 ) ACFZBGZABFZCGZHUSBFZUSBDIZJZBKZDFZHZV
      ACFZVACEIZJZCKZEFZHZHVCVIHZVGVMHZHVCAVDVJHJZCKZBKZEFDFZHUTVHVBVNUSBDUSDLM
      VACEVAELMNVCVGVIVMOVOVCVPVTVOVCVCHVCVIVCVCACBPQVCRSVTVFVLHZEFDFVPVSWADEVS
      AVDJZCKZAVJJZBKZHZCKZBKZVEVKHZCKBKWAVSWCWDCKZBKZHZBKZWHWCBKZWKHZWNWKBKZHV
      SWMWKWPWNWKWPWJBTWKBUAUBQVSWCWJHZBKWOVRWQBVRWBWDHZCKWQVQWRCAVDVJUCUDWBWDC
      UEUFUDWCWJBUEUFWCWKBUEUGWGWLBWGWCCKZWECKZHWLWCWECUEWSWCWTWKWSWCWCCUAWBCTU
      BWDCBUHNUFUDSWFWIBCWCVEWEVKAVDCUIAVJBUINUJVEVKBCUSVDCACUKBDCULUMVAVJBABUK
      CEBULUMUNUOUPVFVLDEUQURNUO $.
      $([3-Dec-01]$)

    $( Double existential uniqueness. $)
$(
    2eu5 $p |- ( ( E! x E. y ph /\ E! y E. x ph ) <->
               E. z E. w A. x A. y ( ph <-> ( x = z /\ y = w ) ) ) $=
?$.
$)
  $}


