$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Examples of rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
   $d x y z $.
   $( The set of complex numbers is a (unital) ring.  (Contributed by Steve
      Rodriguez, 2-Feb-2007.) $)
   cnring $p |- <. + , x. >. e. Ring $=
     ( vx vy vz caddc cmul cop cring wcel cabl cc cxp wf wa cv co wceq w3a
     wral wrex cvv wb mulex cgr cnaddabl ablgrp ax-mp axaddopr grprnOLD isring
     axmulopr pm3.2i axmulass axdistr adddirt 3jca rgen3 c1 ax1cn ax1id
     mulid2t jca rgen opreq2 eqeq1d opreq1 anbi12d ralbidv rcla4ev mp2an
     mpbir2an ) DEFGHZDIHZJJKJELZMZANZBNZEOZCNZEOVOVPVREOZEOPZVOVPVRDOEOVQVOVRE
     OZDOPZVOVPDOVREOWAVSDOPZQZCJRBJRAJRZVPVOEOZVPPZVQVPPZMZBJRZAJSZMZETHVKVNWL
     MUAUBABCTDEJDJVLDUCHUDDUEUFUGUHUIUFVLVMUDUJUKWEWKWDABCJJJVOJHVPJHZVRJHQVTW
     BWCVOVPVRULVOVPVRUMVOVPVRUNUOUPUQJHVPUQEOZVPPZUQVPEOZVPPZMZBJRZWKURWRBJWMW
     OWQVPUSVPUTVAVBWJWSAUQJVOUQPZWIWRBJWTWGWOWHWQWTWFWNVPVOUQVPEVCVDWTVQWPVPVO
     UQVPEVEVDVFVGVHVIUKVJ $.
     $( [3-Feb-2007] $) $( [22-Nov-2006] $)
  $}

  ${
   $d x y z A $.
   ringsn.1 $e |- A e. V $.
   $( The _trivial_ or _zero_ ring defined on a singleton set ` { A } `
      (see  ~ http://en.wikipedia.org/wiki/Trivial&#x5F;ring ).  (Contributed
      by Steve Rodriguez, 10-Feb-2007.) $)
   ringsn $p |- <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >.
                e. Ring $=
     ( vx vy vz cop csn cring wcel cabl cxp wf wa cv co wceq w3a wral wrex cvv
     wb snex crn opex rnsnop eqcomi isring ax-mp ablsn wfn ffnoprval wfun cdm
     df-fn funsn dmsnop xpsn eqtr4 mpbir2an opreq12 cfv df-opr fvsn eqtr
     syl6eq elsnc2 sylibr elsn syl2anb rgen2a pm3.2i pm3.26 3eqtr4a 3adant3
     pm3.27 eqtr4d 3adant1 opreq12d eqtr3t opreq1d ancoms opreq2d 3jca syl3anb
     rgen3 snid opreq1 id eqeq12d opreq2 anbi12d mpbiri sylbi rgen eqeq1d
     ralbidv rcla4ev mp2an ) AAFZAFZGZXAFHIZXAJIZAGZXDKZXDXALZMZCNZDNZXAOZENZXA
     OZXHXIXKXAOZXAOZPZXNXJXHXKXAOZXAOPZXLXPXMXAOPZQZEXDRDXDRCXDRZXIXHXAOZXIPZX
     JXIPZMZDXDRZCXDSZMZXATIXBXGYGMUAWTUBCDETXAXAXDXAUCXDWSAAAUDZBUEUFUGUHXCXFA
     BUIXFXAXEUJZXJXDIZDXDRCXDRCDXDXDXDXAUKYIXAULXAUMZXEPXAXEUNWSAYHBUOYKWSGXEW
     SAUPAABBUQURUSYJCDXDXHAPZXIAPZYJXHXDIZXIXDIZYLYMMZXJAPYJYPXJAAXAOZAXHAXIAX
     AUTZYQWSXAVAAAAXAVBWSAYHBVCVDZVEZXJABVFVGCAVHZDAVHZVIVJUSVKXTYFXSCDEXDXDXD
     YLYMXKAPZXSYNYOXKXDIYLYMUUCQZXOXQXRUUDXJXHXKXMXAYLYMXJXHPUUCYPYQAXJXHYSYRY
     LYMVLZVMVNYMUUCXKXMPYLYMUUCMZXKAXMYMUUCVOUUFXMYQAXIAXKAXAUTYSVEVPVQZVRUUDX
     HXJXMXPXAYLYMXHXJPUUCYPXHAXJUUEYTVPVNYLYMXMXPPZUUCYMYLUUHYMYLMXIXHXKXAXIXH
     AVSVTWAVNVRUUDXJXPXKXMXAYMUUCXJXPPYLUUFXIXKXHXAXIXKAVSWBVQUUGVRWCUUAUUBEAV
     HWDWEAXDIXIAXAOZXIPZAXIXAOZXIPZMZDXDRZYFABWFUUMDXDYOYMUUMUUBYMUUMYQAPZUUOM
     UUOUUOYSYSVKYMUUJUUOUULUUOYMUUIYQXIAXIAAXAWGYMWHZWIYMUUKYQXIAXIAAXAWJUUPWI
     WKWLWMWNYEUUNCAXDYLYDUUMDXDYLYBUUJYCUULYLYAUUIXIXHAXIXAWJWOYLXJUUKXIXHAXIX
     AWGWOWKWPWQWRVKUS $.
     $( [10-Feb-2007] $) $( [3-Feb-2007] $)
  $}

