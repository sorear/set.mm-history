$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c RingOps $.

  $( Extend class notation with the class of all unital rings. $)
  cring $a class RingOps $.

  ${
    $d g h x y z $.
    $( Define the class of all unital rings. $)
    df-ringo $a |- RingOps = { <. g , h >. | ( ( g e. AbelOp
      /\ h : ( ran g X. ran g ) --> ran g )
      /\ ( A. x e. ran g A. y e. ran g A. z e. ran g ( ( ( x h y
       ) h z ) = ( x h ( y h z ) ) /\ ( x h ( y g z ) ) = ( ( x h y ) g ( x h z
          ) ) /\ ( ( x g y ) h z ) = ( ( x h z ) g ( y h z ) ) ) /\ E. x e. ran
            g A. y e. ran g ( ( y h x ) = y /\ ( x h y ) = y ) ) ) } $.
  $}

  ${
    $d g h x y z G $.  $d g h x y z H $.  $d g h x y z X $.
    isring.1 $e |- X = ran G $.
    $( The predicate "is a (unital) ring."  Definition of ring with unit in
       [Schechter] p. 187.  (Contributed by Jeffrey Hankins, 21-Nov-2006.) $)
    isringo $p |- ( H e. A -> ( <. G , H >. e. RingOps <-> ( ( G e. AbelOp /\ H
 : ( X X. X ) --> X ) /\ ( A. x e. X A. y e. X A. z e. X ( ( ( x H y ) H z ) =
      ( x H ( y H z ) ) /\ ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) /\ ( (
          x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) /\ E. x e. X A. y e. X (
            ( y H x ) = y /\ ( x H y ) = y ) ) ) ) ) $=
      ( vg vh wcel cop cring cabl cxp wf wa cv co wceq w3a wral wrex cvv wbr
      df-br wrel crn copab relopab df-ringo releqi mpbir brrelexi sylbir
      anim1i ancoms elisset adantrr eleq1 wb rneq syl6eqr xpeq1 xpeq2 eqtrd
      feq2d feq3 bitrd syl anbi12d opreq opreq2d eqeq12d opreq1d 3anbi23d
      raleqbidv raleqdv rexeqbidv feq1 anbi2d opreq12d 3anbi123d ralbidv
      2ralbidv eqeq1d rexralbidv opelopabg eleq2i syl5bb pm5.21nd ) FDKZEFLZMKZ
      ENKZGGOZGFPZQZARZBRZFSZCRZFSZWSWTXBFSZFSZTZWSWTXBESZFSZXAWSXBFSZESZTZWSWT
      ESZXBFSZXIXDESZTZUAZCGUBZBGUBAGUBZWTWSFSZWTTZXAWTTZQZBGUBAGUCZQZQZEUDKZWL
      QZWNWLYGWNYFWLWNEFMUEYFEFMUFEFMMUGIRZNKZYHUHZYJOZYJJRZPZQZWSWTYLSZXBYLSZW
      SWTXBYLSZYLSZTZWSWTXBYHSZYLSZYOWSXBYLSZYHSZTZWSWTYHSZXBYLSZUUBYQYHSZTZUAZ
      CYJUBZBYJUBZAYJUBZWTWSYLSZWTTZYOWTTZQZBYJUBZAYJUCZQZQZIJUIZUGUUTIJUJMUVAA
      BCIJUKZULUMUNUOUPUQWLWRYGYDWLWOYGWQWOWLYGWOYFWLENURUPUQUSUSYGWMUVAKYEWNUU
      TWOWPGYLPZQZYSWSXGYLSZYOUUBESZTZXLXBYLSZUUBYQESZTZUAZCGUBZBGUBZAGUBZUUPBG
      UBZAGUCZQZQYEIJEFUDDYHETZYNUVDUUSUVQUVRYIWOYMUVCYHENUTUVRYJGTZYMUVCVAUVRY
      JEUHGYHEVBHVCZUVSYMWPYJYLPUVCUVSYKWPYJYLUVSYKGYJOWPYJGYJVDYJGGVEVFVGYJGWP
      YLVHVIVJVKUVRUULUVNUURUVPUVRUUKUVMAYJGUVTUVRUUJUVLBYJGUVTUVRUUIUVKCYJGUVT
      UVRUUDUVGUUHUVJYSUVRUUAUVEUUCUVFUVRYTXGWSYLWTXBYHEVLVMYOUUBYHEVLVNUVRUUFU
      VHUUGUVIUVRUUEXLXBYLWSWTYHEVLVOUUBYQYHEVLVNVPVQVQVQUVRUUQUVOAYJGUVTUVRUUP
      BYJGUVTVRVSVKVKYLFTZUVDWRUVQYDUWAUVCWQWOWPGYLFVTWAUWAUVNXRUVPYCUWAUVLXQAB
      GGUWAUVKXPCGUWAYSXFUVGXKUVJXOUWAYPXCYRXEUWAYPYOXBFSXCYOXBYLFVLUWAYOXAXBFW
      SWTYLFVLZVOVFUWAYRWSYQFSXEWSYQYLFVLUWAYQXDWSFWTXBYLFVLZVMVFVNUWAUVEXHUVFX
      JWSXGYLFVLUWAYOXAUUBXIEUWBWSXBYLFVLZWBVNUWAUVHXMUVIXNXLXBYLFVLUWAUUBXIYQX
      DEUWDUWCWBVNWCWDWEUWAUUPYBABGGUWAUUNXTUUOYAUWAUUMXSWTWTWSYLFVLWFUWAYOXAWT
      UWBWFVKWGVKVKWHMUVAWMUVBWIWJWK $.
      $( [21-Nov-2006] $)
  $}

  ${
    $d G g h x y z $.  $d H g h x y z $.  $d R g h $.  $d X g h x y z $.
    ringi.1 $e |- G = ( 1st ` R ) $.
    ringi.2 $e |- H = ( 2nd ` R ) $.
    ringi.3 $e |- X = ran G $.
    $( The properties of a unital ring.  (Contributed by Steve Rodriguez,
       8-Sep-2007.) $)
    ringoi $p |- ( R e. RingOps -> ( ( G e. AbelOp /\ H : ( X X. X ) --> X ) /\
     ( A. x e. X A. y e. X A. z e. X ( ( ( x H y ) H z ) = ( x H ( y H z ) ) /\
                 ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) /\
                 ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) /\
                E. x e. X A. y e. X ( ( y H x ) = y /\ ( x H y ) = y ) ) ) ) $=
      ( vg vh cring wcel cv cabl crn cxp wf wa co wceq w3a wral wrex copab
      df-ringo eleq2i c1st cfv wb eqeq2i eleq1 rneq syl6eqr xpeq1 xpeq2 eqtrd
      feq2d feq3 bitrd syl anbi12d raleq raleqbi1dv opreq opreq2d eqeq12d
      opreq1d 3anbi23d ralbidv 2ralbidv raleqdv rexeqbidv sylbir c2nd feq1
      anbi2d opreq12d 3anbi123d eqeq1d rexralbidv elopabi sylbi ) DMNDKOZPNZWEQ
      ZWGRZWGLOZSZTZAOZBOZWIUAZCOZWIUAZWLWMWOWIUAZWIUAZUBZWLWMWOWEUAZWIUAZWNWLW
      OWIUAZWEUAZUBZWLWMWEUAZWOWIUAZXBWQWEUAZUBZUCZCWGUDZBWGUDZAWGUDZWMWLWIUAZW
      MUBZWNWMUBZTZBWGUDZAWGUEZTZTZKLUFZNEPNZGGRZGFSZTZWLWMFUAZWOFUAZWLWMWOFUAZ
      FUAZUBZWLWMWOEUAZFUAZYFWLWOFUAZEUAZUBZWLWMEUAZWOFUAZYMYHEUAZUBZUCZCGUDZBG
      UDAGUDZWMWLFUAZWMUBZYFWMUBZTZBGUDAGUEZTZTZMYADABCKLUGUHXTYBYCGWISZTZWSWLY
      KWIUAZWNXBEUAZUBZYPWOWIUAZXBWQEUAZUBZUCZCGUDZBGUDAGUDZXPBGUDZAGUEZTZTZUUI
      KLDWEDUIUJZUBWEEUBZXTUVDUKEUVEWEHULUVFWKUUKXSUVCUVFWFYBWJUUJWEEPUMUVFWGGU
      BZWJUUJUKUVFWGEQGWEEUNJUOZUVGWJYCWGWISUUJUVGWHYCWGWIUVGWHGWGRYCWGGWGUPWGG
      GUQURUSWGGYCWIUTVAVBVCUVFXLUUTXRUVBUVFXLXICGUDZBGUDZAGUDZUUTUVFUVGXLUVKUK
      UVHXKUVJAWGGXJUVIBWGGXICWGGVDVEVEVBUVFUVIUUSABGGUVFXIUURCGUVFXDUUNXHUUQWS
      UVFXAUULXCUUMUVFWTYKWLWIWMWOWEEVFVGWNXBWEEVFVHUVFXFUUOXGUUPUVFXEYPWOWIWLW
      MWEEVFVIXBWQWEEVFVHVJVKVLVAUVFXQUVAAWGGUVHUVFXPBWGGUVHVMVNVCVCVOWIDVPUJZU
      BWIFUBZUVDUUIUKFUVLWIIULUVMUUKYEUVCUUHUVMUUJYDYBYCGWIFVQVRUVMUUTUUBUVBUUG
      UVMUUSUUAABGGUVMUURYTCGUVMWSYJUUNYOUUQYSUVMWPYGWRYIUVMWPWNWOFUAYGWNWOWIFV
      FUVMWNYFWOFWLWMWIFVFZVIURUVMWRWLWQFUAYIWLWQWIFVFUVMWQYHWLFWMWOWIFVFZVGURV
      HUVMUULYLUUMYNWLYKWIFVFUVMWNYFXBYMEUVNWLWOWIFVFZVSVHUVMUUOYQUUPYRYPWOWIFV
      FUVMXBYMWQYHEUVPUVOVSVHVTVKVLUVMXPUUFABGGUVMXNUUDXOUUEUVMXMUUCWMWMWLWIFVF
      WAUVMWNYFWMUVNWAVCWBVCVCVOWCWD $.
      $( [8-Sep-2007] $)
  $}

  ${
    $d G x y z $.  $d H x y z $.  $d X x y z $.
    ringsm.1 $e |- G = ( 1st ` R ) $.
    ringsm.2 $e |- H = ( 2nd ` R ) $.
    ringsm.3 $e |- X = ran G $.
    $( Functionality of the multiplication operation of a ring.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringsm $p |- ( R e. RingOps -> H : ( X X. X ) --> X ) $=
      ( vx vy vz cring wcel cabl cxp wf wa cv co wceq w3a wral wrex ringoi
      simpld simprd ) AKLZBMLZDDNDCOZUFUGUHPHQZIQZCRZJQZCRUIUJULCRZCRSUIUJULBRC
      RUKUIULCRZBRSUIUJBRULCRUNUMBRSTJDUAIDUAHDUAUJUICRUJSUKUJSPIDUAHDUBPHIJABC
      DEFGUCUDUE $.
      $( [9-Sep-2007] $)
  $}

  ${
    ringcl.1 $e |- G = ( 1st ` R ) $.
    ringcl.2 $e |- H = ( 2nd ` R ) $.
    ringcl.3 $e |- X = ran G $.
    $( Closure of the multiplication operation of a ring.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ringocl $p |- ( ( R e. RingOps /\ A e. X /\ B e. X ) -> ( A H B ) e. X ) $=
      ( cxp wf wcel co cring foprrn ringsm syl3an1 ) FFJFEKAFLBFLABEMFLCNLABFFF
      EOCDEFGHIPQ $.
      $( [9-Sep-2007] $)
  $}

  ${
    $d A u x $.  $d G u x y $.  $d H u w x y $.  $d R x $.  $d X w u x y $.
    ringid.1 $e |- G = ( 1st ` R ) $.
    ringid.2 $e |- H = ( 2nd ` R ) $.
    ringid.3 $e |- X = ran G $.
    $( The multiplication operation of a unital ring has (one or more) identity
       elements.  (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringid $p |- ( ( R e. RingOps /\ A e. X ) ->
                 E. u e. X ( ( A H u ) = A /\ ( u H A ) = A ) ) $=
      ( vx vy wcel cring cv co wceq wa wrex wi opreq1 id eqeq12d opreq2
      anbi12d rexbidv imbi2d wral w3a cabl cxp wf ringoi simprd r19.12 ra4
      3syl com12 vtoclga impcom ) BFLCMLZBANZEOZBPZVABEOZBPZQZAFRZUTJNZVAEOZVHP
      ZVAVHEOZVHPZQZAFRZSUTVGSJBFVHBPZVNVGUTVOVMVFAFVOVJVCVLVEVOVIVBVHBVHBVAETV
      OUAZUBVOVKVDVHBVHBVAEUCVPUBUDUEUFUTVHFLZVNUTVMJFUGAFRZVNJFUGVQVNSUTVKKNZE
      OVAVHVSEOZEOPVAVHVSDOEOVKVAVSEOZDOPVAVHDOVSEOWAVTDOPUHKFUGJFUGAFUGZVRUTDU
      ILFFUJFEUKQWBVRQAJKCDEFGHIULUMUMVMAJFFUNVNJFUOUPUQURUS $.
      $( [8-Sep-2007] $)

    $( The unit element of a ring is unique. $)
    ringoideu $p |- ( R e. RingOps ->
                 E! u e. X A. x e. X ( ( x H u ) = x /\ ( u H x ) = x ) ) $=
      ( vw vy cring wcel cv co wceq wa wral wrex wi wreu cabl cxp wf w3a
      ringoi simprr syl opreq1 id eqeq12d rcla4v simpl ralimi syl5 opreq2
      simpr im2anan9 eqtr2 ancoms syl6 rgen2a jctir eqeq1d anbi12d ralbidv
      reu4 sylibr ) CLMZANZBNZEOZVJPZVKVJEOZVJPZQZAFRZBFSZVQVJJNZEOZVJPZVSVJEOZ
      VJPZQZAFRZQZVKVSPZTZJFRBFRZQVQBFUAVIVRWIVIDUBMFFUCFEUDQZVNKNZEOVKVJWKEOZE
      OPVKVJWKDOEOVNVKWKEOZDOPVKVJDOWKEOWMWLDOPUEKFRAFRBFRZVRQQVRBAKCDEFGHIUFWJ
      WNVRUGUHWHBJFVSFMZVKFMZWHWOWPQWFVSVKEOZVSPZWQVKPZQWGWOVQWRWPWEWSWOVMAFRWR
      VQVMWRAVSFVJVSPZVLWQVJVSVJVSVKEUIWTUJUKULVPVMAFVMVOUMUNUOWPWCAFRWSWEWCWSA
      VKFVJVKPZWBWQVJVKVJVKVSEUPXAUJUKULWDWCAFWAWCUQUNUOURWSWRWGWQVKVSUSUTVAUTV
      BVCVQWEBJFWGVPWDAFWGVMWAVOWCWGVLVTVJVKVSVJEUPVDWGVNWBVJVKVSVJEUIVDVEVFVGV
      H $.
      $( [4-Apr-2009] $)
  $}

  ${
    $d A x y z $.  $d B y z $.  $d C z $.  $d G x y z $.  $d H x y z $.
    $d X x y z $.
    ringdi.1 $e |- G = ( 1st ` R ) $.
    ringdi.2 $e |- H = ( 2nd ` R ) $.
    ringdi.3 $e |- X = ran G $.
    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringodi $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                     ( A H ( B G C ) ) = ( ( A H B ) G ( A H C ) ) ) $=
      ( vx vy vz wcel w3a cring co wceq cv wral opreq1 opreq12d eqeq12d
      opreq2d opreq2 opreq1d rcla43v simp2 ralimi syl5 wa wrex cabl cxp wf
      ringoi simprd simpld impcom ) AGNBGNCGNOZDPNZABCEQZFQZABFQZACFQZEQZRZUTKS
      ZLSZFQZMSZFQVHVIVKFQZFQRZVHVIVKEQZFQZVJVHVKFQZEQZRZVHVIEQVKFQVPVLEQRZOZMG
      TZLGTZKGTZVGVAUTVRMGTZLGTZKGTVGWCVRVGAVNFQZAVIFQZAVKFQZEQZRABVKEQZFQZVDWH
      EQZRKLMABCGGGVHARZVOWFVQWIVHAVNFUAWMVJWGVPWHEVHAVIFUAVHAVKFUAUBUCVIBRZWFW
      KWIWLWNVNWJAFVIBVKEUAUDWNWGVDWHEVIBAFUEUFUCVKCRZWKVCWLVFWOWJVBAFVKCBEUEUD
      WOWHVEVDEVKCAFUEUDUCUGWBWEKGWAWDLGVTVRMGVMVRVSUHUIUIUIUJVAWCVIVHFQVIRVJVI
      RUKLGTKGULZVAEUMNGGUNGFUOUKWCWPUKKLMDEFGHIJUPUQURUJUS $.
      $( [9-Sep-2007] $)

    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringodir $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A G B ) H C ) = ( ( A H C ) G ( B H C ) ) ) $=
      ( vx vy vz wcel w3a cring co wceq cv wral opreq1 opreq1d eqeq12d opreq2
      opreq2d opreq12d rcla43v simp3 ralimi syl5 wa wrex cabl cxp wf ringoi
      simprd simpld impcom ) AGNBGNCGNOZDPNZABEQZCFQZACFQZBCFQZEQZRZUTKSZLSZFQZ
      MSZFQVHVIVKFQZFQRZVHVIVKEQFQVJVHVKFQZEQRZVHVIEQZVKFQZVNVLEQZRZOZMGTZLGTZK
      GTZVGVAUTVSMGTZLGTZKGTVGWCVSVGAVIEQZVKFQZAVKFQZVLEQZRVBVKFQZWHBVKFQZEQZRK
      LMABCGGGVHARZVQWGVRWIWMVPWFVKFVHAVIEUAUBWMVNWHVLEVHAVKFUAUBUCVIBRZWGWJWIW
      LWNWFVBVKFVIBAEUDUBWNVLWKWHEVIBVKFUAUEUCVKCRZWJVCWLVFVKCVBFUDWOWHVDWKVEEV
      KCAFUDVKCBFUDUFUCUGWBWEKGWAWDLGVTVSMGVMVOVSUHUIUIUIUJVAWCVIVHFQVIRVJVIRUK
      LGTKGULZVAEUMNGGUNGFUOUKWCWPUKKLMDEFGHIJUPUQURUJUS $.
      $( [9-Sep-2007] $)

    $( Associative law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringoass $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A H B ) H C ) = ( A H ( B H C ) ) ) $=
      ( vx vy vz wcel w3a cring co wceq cv wral opreq1 opreq1d eqeq12d opreq2
      opreq2d rcla43v wa wrex cabl cxp wf ringoi simprd simpld simp1 ralimi
      syl syl5 impcom ) AGNBGNCGNOZDPNZABFQZCFQZABCFQZFQZRZUTKSZLSZFQZMSZFQZVGV
      HVJFQZFQZRZMGTZLGTZKGTZVFVAVNVFAVHFQZVJFQZAVLFQZRVBVJFQZABVJFQZFQZRKLMABC
      GGGVGARZVKVSVMVTWDVIVRVJFVGAVHFUAUBVGAVLFUAUCVHBRZVSWAVTWCWEVRVBVJFVHBAFU
      DUBWEVLWBAFVHBVJFUAUEUCVJCRZWAVCWCVEVJCVBFUDWFWBVDAFVJCBFUDUEUCUFVAVNVGVH
      VJEQFQVIVGVJFQZEQRZVGVHEQVJFQWGVLEQRZOZMGTZLGTZKGTZVQVAWMVHVGFQVHRVIVHRUG
      LGTKGUHZVAEUINGGUJGFUKUGWMWNUGKLMDEFGHIJULUMUNWLVPKGWKVOLGWJVNMGVNWHWIUOU
      PUPUPUQURUS $.
      $( [9-Sep-2007] $)
  $}

  ${
    $d A x $.  $d G x $.  $d H x $.  $d R x $.  $d X x $.
    ring2.1 $e |- G = ( 1st ` R ) $.
    ring2.2 $e |- H = ( 2nd ` R ) $.
    ring2.3 $e |- X = ran G $.
    $( A ring element plus itself is two times the element.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ring2 $p |- ( ( R e. RingOps /\ A e. X ) ->
                  E. x e. X ( A G A ) = ( ( x G x ) H A ) ) $=
      ( cring wcel wa cv co wceq wrex ringid simpr reximi opreq12 anidms 3syl
      wi w3a ringodir expcom 3expia 3imp 3com13 eqtr eqcomd sylan ex 3expa
      reximdva mpd ) CJKZBFKZLZAMZBENZVADNZBBDNZOZAFPZVCUTUTDNBENZOZAFPUSBUTENB
      OZVABOZLZAFPVIAFPVEABCDEFGHIQVJVIAFVHVIRSVIVDAFVIVDVABVABDTUASUBUSVDVGAFU
      QURUTFKZVDVGUCUQURVKUDZVDVGVLVFVBOZVDVGVKURUQVMVKURUQVMVKURUQVMUCZUCVKVKU
      RVNUQVKVKURUDVMUTUTBCDEFGHIUEUFUGUAUHUIVMVDLVFVCVFVBVCUJUKULUMUNUOUP $.
      $( [9-Sep-2007] $)
  $}

  ${
    $d G x y z $.  $d R x y z $.
    ringabl.1 $e |- G = ( 1st ` R ) $.
    $( A ring's addition operation is an Abelian group operation.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringoablo $p |- ( R e. RingOps -> G e. AbelOp ) $=
      ( vx vy vz cring wcel cabl crn cxp c2nd cfv wf wa cv co wceq w3a wral
      wrex eqid ringoi simpld ) AGHZBIHZBJZUGKUGALMZNZUEUFUIODPZEPZUHQZFPZUHQUJ
      UKUMUHQZUHQRUJUKUMBQUHQULUJUMUHQZBQRUJUKBQUMUHQUOUNBQRSFUGTEUGTDUGTUKUJUH
      QUKRULUKROEUGTDUGUAODEFABUHUGCUHUBUGUBUCUDUD $.
      $( [9-Sep-2007] $)
  $}

  ${
    ringgrp.1 $e |- G = ( 1st ` R ) $.
    $( A ring's addition operation is a group operation.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ringogrpo $p |- ( R e. RingOps -> G e. GrpOp ) $=
      ( cring wcel cabl cgr ringoablo ablogrpo syl ) ADEBFEBGEABCHBIJ $.
      $( [9-Sep-2007] $)
  $}

  ${
    ringgcl.1 $e |- G = ( 1st ` R ) $.
    ringgcl.2 $e |- X = ran G $.
    $( Closure law for the addition (group) operation of a ring.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringgcl $p |- ( ( R e. RingOps /\ A e. X /\ B e. X ) -> ( A G B ) e. X ) $=
      ( cgr wcel co cring grpocl ringogrpo syl3an1 ) DHIAEIBEIABDJEICKIABDEGLCD
      FMN $.
      $( [9-Sep-2007] $)

    $( The addition operation of a ring is commutative.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ringcom $p |- ( ( R e. RingOps /\ A e. X /\ B e. X ) ->
                      ( A G B ) = ( B G A ) ) $=
      ( cabl wcel co wceq cring ablocom ringoablo syl3an1 ) DHIAEIBEIABDJBADJKC
      LIABDEGMCDFNO $.
      $( [9-Sep-2007] $)

    $( The addition operation of a ring is associative.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ringaass $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                        ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( cring wcel cgr w3a co wceq ringogrpo grpoass sylan ) DIJEKJAFJBFJCFJLAB
      EMCEMABCEMEMNDEGOABCEFHPQ $.
      $( [9-Sep-2007] $)

    $( The addition operation of a ring is commutative.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ringa23 $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cring wcel cabl w3a co wceq ringoablo abl23 sylan ) DIJEKJAFJBFJCFJLABE
      MCEMACEMBEMNDEGOABCEFHPQ $.
      $( [9-Sep-2007] $)

    $( Rearrangement of 4 terms in a sum of ring elements.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ringa4 $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X ) /\
                   ( C e. X /\ D e. X ) ) ->
                     ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( cabl wcel wa co wceq cring abl4 ringoablo syl3an1 ) FJKAGKBGKLCGKDGKLAB
      FMCDFMFMACFMBDFMFMNEOKABCDFGIPEFHQR $.
      $( [9-Sep-2007] $)

    $( Right cancellation law for the addition operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringrcan $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                       ( ( A G C ) = ( B G C ) <-> A = B ) ) $=
      ( cring wcel cgr w3a co wceq wb ringogrpo grporcan sylan ) DIJEKJAFJBFJCF
      JLACEMBCEMNABNODEGPABCEFHQR $.
      $( [9-Sep-2007] $)

    $( Left cancellation law for the addition operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringlcan $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                       ( ( C G A ) = ( C G B ) <-> A = B ) ) $=
      ( cring wcel cgr w3a co wceq wb ringogrpo grpolcan sylan ) DIJEKJAFJBFJCF
      JLCAEMCBEMNABNODEGPABCEFHQR $.
      $( [9-Sep-2007] $)
  $}

  ${
    ring0cl.1 $e |- G = ( 1st ` R ) $.
    ring0cl.2 $e |- X = ran G $.
    ring0cl.3 $e |- Z = ( Id ` G ) $.
    $( A ring has an additive identity element.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ring0cl $p |- ( R e. RingOps -> Z e. X ) $=
      ( cring wcel cgr ringogrpo grpoidcl syl ) AHIBJIDCIABEKDBCFGLM $.
      $( [9-Sep-2007] $)

    $( The additive identity of a ring is a right identity element.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ring0rid $p |- ( ( R e. RingOps /\ A e. X ) -> ( A G Z ) = A ) $=
      ( cring wcel cgr co wceq ringogrpo grporid sylan ) BIJCKJADJAECLAMBCFNAEC
      DGHOP $.
      $( [9-Sep-2007] $)

    $( The additive identity of a ring is a left identity element.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ring0lid $p |- ( ( R e. RingOps /\ A e. X ) -> ( Z G A ) = A ) $=
      ( cring wcel cgr co wceq ringogrpo grpolid sylan ) BIJCKJADJEACLAMBCFNAEC
      DGHOP $.
      $( [9-Sep-2007] $)
  $}

  ${
    ringlz.1 $e |- Z = ( Id ` G ) $.
    ringlz.2 $e |- X = ran G $.
    ringlz.3 $e |- G = ( 1st ` R ) $.
    ringlz.4 $e |- H = ( 2nd ` R ) $.
    $( The zero of a unital ring is a left absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    ringolz $p |- ( ( R e. RingOps /\ A e. X ) -> ( Z H A ) = Z ) $=
      ( cring wcel wa co wceq cgr ringogrpo grpoidcl grpolid mpdan syl adantr
      opreq1d w3a ring0cl simpr 3jca ringodir syldan simpl ringocl syl111anc
      grporid eqcomd syl11anc 3eqtr3d wb grpolcan syl13anc mpbid ) BKLZAELZMZFA
      DNZVDCNZVDFCNZOZVDFOZVCFFCNZADNZVDVEVFVCVIFADVAVIFOZVBVACPLZVKBCIQZVLFELZ
      VKFCEHGRFFCEHGSTUAUBUCVAVBVNVNVBUDVJVEOVCVNVNVBVAVNVBBCEFIHGUEUBZVOVAVBUF
      ZUGFFABCDEIJHUHUIVCVLVDELZVDVFOVAVLVBVMUBZVCVAVNVBVQVAVBUJVOVPFABCDEIJHUK
      ULZVLVQMVFVDVDFCEHGUMUNUOUPVCVLVQVNVQVGVHUQVRVSVOVSVDFVDCEHURUSUT $.
      $( [31-Aug-2009] $)

    $( The zero of a unital ring is a right absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    ringorz $p |- ( ( R e. RingOps /\ A e. X ) -> ( A H Z ) = Z ) $=
      ( cring wcel wa co wceq cgr ringogrpo grpoidcl grpolid mpdan syl adantr
      opreq2d w3a simpr ring0cl 3jca ringodi syldan ringocl mpd3an3 eqcomd
      syl11anc 3eqtr3d wb grporcan syl13anc mpbid ) BKLZAELZMZAFDNZVBCNZFVBCNZO
      ZVBFOZVAAFFCNZDNZVBVCVDVAVGFADUSVGFOZUTUSCPLZVIBCIQZVJFELZVIFCEHGRFFCEHGS
      TUAUBUCUSUTUTVLVLUDVHVCOVAUTVLVLUSUTUEUSVLUTBCEFIHGUFUBZVMUGAFFBCDEIJHUHU
      IVAVJVBELZVBVDOUSVJUTVKUBZUSUTVLVNVMAFBCDEIJHUJUKZVJVNMVDVBVBFCEHGSULUMUN
      VAVJVNVLVNVEVFUOVOVPVMVPVBFVBCEHUPUQUR $.
      $( [31-Aug-2009] $)
  $}

