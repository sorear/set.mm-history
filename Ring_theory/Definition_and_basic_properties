$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Ring $.

  $( Extend class notation with the class of all unital rings. $)
  cring $a class Ring $.

  ${
    $d g h x y z $.
    $( Define the class of all unital rings. $)
    df-ring $a |- Ring = { <. g , h >. | ( ( g e. Abel /\ h : ( ran g X. ran g
      ) --> ran g ) /\ ( A. x e. ran g A. y e. ran g A. z e. ran g ( ( ( x h y
       ) h z ) = ( x h ( y h z ) ) /\ ( x h ( y g z ) ) = ( ( x h y ) g ( x h z
          ) ) /\ ( ( x g y ) h z ) = ( ( x h z ) g ( y h z ) ) ) /\ E. x e. ran
            g A. y e. ran g ( ( y h x ) = y /\ ( x h y ) = y ) ) ) } $.
  $}

  ${
    $d g h x y z G $.  $d g h x y z H $.  $d g h x y z X $.
    isring.1 $e |- X = ran G $.
    $( The predicate "is a (unital) ring."  Definition of ring with unit in
       [Schechter] p. 187.  (Contributed by Jeffrey Hankins, 21-Nov-2006.) $)
    isring $p |- ( H e. A -> ( <. G , H >. e. Ring <-> ( ( G e. Abel /\ H : ( X
      X. X ) --> X ) /\ ( A. x e. X A. y e. X A. z e. X ( ( ( x H y ) H z ) = (
        x H ( y H z ) ) /\ ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) /\ ( (
          x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) /\ E. x e. X A. y e. X (
            ( y H x ) = y /\ ( x H y ) = y ) ) ) ) ) $=
      ( vg vh wcel cop cring cabl cxp wf wa cv co wceq w3a wral wrex cvv wbr
      df-br wrel crn copab relopab df-ring releqi mpbir brrelexi sylbir anim1i
      ancoms elisset adantrr eleq1 wb rneq syl6eqr xpeq1 xpeq2 eqtrd feq2 syl
      feq3 bitrd anbi12d opreq opreq2d eqeq12d opreq1d 3anbi23d raleq12d
      raleq1d rexeq12d feq1 anbi2d opreq12d 3anbi123d ralbidv 2ralbidv eqeq1d
      rexralbidv opelopabg eleq2i syl5bb pm5.21nd ) FDKZEFLZMKZENKZGGOZGFPZQZAR
      ZBRZFSZCRZFSZWSWTXBFSZFSZTZWSWTXBESZFSZXAWSXBFSZESZTZWSWTESZXBFSZXIXDESZT
      ZUAZCGUBZBGUBAGUBZWTWSFSZWTTZXAWTTZQZBGUBAGUCZQZQZEUDKZWLQZWNWLYGWNYFWLWN
      EFMUEYFEFMUFEFMMUGIRZNKZYHUHZYJOZYJJRZPZQZWSWTYLSZXBYLSZWSWTXBYLSZYLSZTZW
      SWTXBYHSZYLSZYOWSXBYLSZYHSZTZWSWTYHSZXBYLSZUUBYQYHSZTZUAZCYJUBZBYJUBZAYJU
      BZWTWSYLSZWTTZYOWTTZQZBYJUBZAYJUCZQZQZIJUIZUGUUTIJUJMUVAABCIJUKZULUMUNUOU
      PUQWLWRYGYDWLWOYGWQWOWLYGWOYFWLENURUPUQUSUSYGWMUVAKYEWNUUTWOWPGYLPZQZYSWS
      XGYLSZYOUUBESZTZXLXBYLSZUUBYQESZTZUAZCGUBZBGUBZAGUBZUUPBGUBZAGUCZQZQYEIJE
      FUDDYHETZYNUVDUUSUVQUVRYIWOYMUVCYHENUTUVRYJGTZYMUVCVAUVRYJEUHGYHEVBHVCZUV
      SYMWPYJYLPZUVCUVSYKWPTYMUWAVAUVSYKGYJOWPYJGYJVDYJGGVEVFYKWPYJYLVGVHYJGWPY
      LVIVJVHVKUVRUULUVNUURUVPUVRUUKUVMAYJGUVTUVRUUJUVLBYJGUVTUVRUUIUVKCYJGUVTU
      VRUUDUVGUUHUVJYSUVRUUAUVEUUCUVFUVRYTXGWSYLWTXBYHEVLVMYOUUBYHEVLVNUVRUUFUV
      HUUGUVIUVRUUEXLXBYLWSWTYHEVLVOUUBYQYHEVLVNVPVQVQVQUVRUUQUVOAYJGUVTUVRUUPB
      YJGUVTVRVSVKVKYLFTZUVDWRUVQYDUWBUVCWQWOWPGYLFVTWAUWBUVNXRUVPYCUWBUVLXQABG
      GUWBUVKXPCGUWBYSXFUVGXKUVJXOUWBYPXCYRXEUWBYPYOXBFSXCYOXBYLFVLUWBYOXAXBFWS
      WTYLFVLZVOVFUWBYRWSYQFSXEWSYQYLFVLUWBYQXDWSFWTXBYLFVLZVMVFVNUWBUVEXHUVFXJ
      WSXGYLFVLUWBYOXAUUBXIEUWCWSXBYLFVLZWBVNUWBUVHXMUVIXNXLXBYLFVLUWBUUBXIYQXD
      EUWEUWDWBVNWCWDWEUWBUUPYBABGGUWBUUNXTUUOYAUWBUUMXSWTWTWSYLFVLWFUWBYOXAWTU
      WCWFVKWGVKVKWHMUVAWMUVBWIWJWK $.
      $( [22-Nov-2006] $) $( [21-Nov-2006] $)
  $}

  ${
    $d G g h x y z $.  $d H g h x y z $.  $d R g h $.  $d X g h x y z $.
    ringi.1 $e |- G = ( 1st ` R ) $.
    ringi.2 $e |- H = ( 2nd ` R ) $.
    ringi.3 $e |- X = ran G $.
    $( The properties of a unital ring.  (Contributed by Steve Rodriguez,
       8-Sep-2007.) $)
    ringi $p |- ( R e. Ring -> ( ( G e. Abel /\ H : ( X X. X ) --> X ) /\
     ( A. x e. X A. y e. X A. z e. X ( ( ( x H y ) H z ) = ( x H ( y H z ) ) /\
                 ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) /\
                 ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) /\
                E. x e. X A. y e. X ( ( y H x ) = y /\ ( x H y ) = y ) ) ) ) $=
      ( vg vh cring wcel cv cabl crn cxp wf wa co wceq w3a wral wrex copab
      df-ring eleq2i c1st cfv wb eqeq2i eleq1 rneq syl6eqr xpeq1 xpeq2 eqtrd
      feq2 syl feq3 bitrd anbi12d raleq1 raleqd opreq opreq2d eqeq12d opreq1d
      3anbi23d ralbidv 2ralbidv raleq1d rexeq12d sylbir c2nd feq1 anbi2d
      opreq12d 3anbi123d eqeq1d rexralbidv elopabi sylbi ) DMNDKOZPNZWEQZWGRZWG
      LOZSZTZAOZBOZWIUAZCOZWIUAZWLWMWOWIUAZWIUAZUBZWLWMWOWEUAZWIUAZWNWLWOWIUAZW
      EUAZUBZWLWMWEUAZWOWIUAZXBWQWEUAZUBZUCZCWGUDZBWGUDZAWGUDZWMWLWIUAZWMUBZWNW
      MUBZTZBWGUDZAWGUEZTZTZKLUFZNEPNZGGRZGFSZTZWLWMFUAZWOFUAZWLWMWOFUAZFUAZUBZ
      WLWMWOEUAZFUAZYFWLWOFUAZEUAZUBZWLWMEUAZWOFUAZYMYHEUAZUBZUCZCGUDZBGUDAGUDZ
      WMWLFUAZWMUBZYFWMUBZTZBGUDAGUEZTZTZMYADABCKLUGUHXTYBYCGWISZTZWSWLYKWIUAZW
      NXBEUAZUBZYPWOWIUAZXBWQEUAZUBZUCZCGUDZBGUDAGUDZXPBGUDZAGUEZTZTZUUIKLDWEDU
      IUJZUBWEEUBZXTUVDUKEUVEWEHULUVFWKUUKXSUVCUVFWFYBWJUUJWEEPUMUVFWGGUBZWJUUJ
      UKUVFWGEQGWEEUNJUOZUVGWJYCWGWISZUUJUVGWHYCUBWJUVIUKUVGWHGWGRYCWGGWGUPWGGG
      UQURWHYCWGWIUSUTWGGYCWIVAVBUTVCUVFXLUUTXRUVBUVFXLXICGUDZBGUDZAGUDZUUTUVFU
      VGXLUVLUKUVHXKUVKAWGGXJUVJBWGGXICWGGVDVEVEUTUVFUVJUUSABGGUVFXIUURCGUVFXDU
      UNXHUUQWSUVFXAUULXCUUMUVFWTYKWLWIWMWOWEEVFVGWNXBWEEVFVHUVFXFUUOXGUUPUVFXE
      YPWOWIWLWMWEEVFVIXBWQWEEVFVHVJVKVLVBUVFXQUVAAWGGUVHUVFXPBWGGUVHVMVNVCVCVO
      WIDVPUJZUBWIFUBZUVDUUIUKFUVMWIIULUVNUUKYEUVCUUHUVNUUJYDYBYCGWIFVQVRUVNUUT
      UUBUVBUUGUVNUUSUUAABGGUVNUURYTCGUVNWSYJUUNYOUUQYSUVNWPYGWRYIUVNWPWNWOFUAY
      GWNWOWIFVFUVNWNYFWOFWLWMWIFVFZVIURUVNWRWLWQFUAYIWLWQWIFVFUVNWQYHWLFWMWOWI
      FVFZVGURVHUVNUULYLUUMYNWLYKWIFVFUVNWNYFXBYMEUVOWLWOWIFVFZVSVHUVNUUOYQUUPY
      RYPWOWIFVFUVNXBYMWQYHEUVQUVPVSVHVTVKVLUVNXPUUFABGGUVNXNUUDXOUUEUVNXMUUCWM
      WMWLWIFVFWAUVNWNYFWMUVOWAVCWBVCVCVOWCWD $.
      $( [14-Sep-2007] $) $( [8-Sep-2007] $)
  $}

  ${
    $d G x y z $.  $d H x y z $.  $d X x y z $.
    ringsm.1 $e |- G = ( 1st ` R ) $.
    ringsm.2 $e |- H = ( 2nd ` R ) $.
    ringsm.3 $e |- X = ran G $.
    $( Functionality of the multiplication operation of a ring.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringsm $p |- ( R e. Ring -> H : ( X X. X ) --> X ) $=
      ( vx vy vz cring wcel cabl cxp wf wa cv co wceq w3a wral wrex ringi
      pm3.26d pm3.27d ) AKLZBMLZDDNDCOZUFUGUHPHQZIQZCRZJQZCRUIUJULCRZCRSUIUJULB
      RCRUKUIULCRZBRSUIUJBRULCRUNUMBRSTJDUAIDUAHDUAUJUICRUJSUKUJSPIDUAHDUBPHIJA
      BCDEFGUCUDUE $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)
  $}

  ${
    ringcl.1 $e |- G = ( 1st ` R ) $.
    ringcl.2 $e |- H = ( 2nd ` R ) $.
    ringcl.3 $e |- X = ran G $.
    $( Closure of the multiplication operation of a ring.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringcl $p |- ( ( R e. Ring /\ A e. X /\ B e. X ) -> ( A H B ) e. X ) $=
      ( cxp wf wcel co cring foprrn ringsm syl3an1 ) FFJFEKAFLBFLABEMFLCNLABFFF
      EOCDEFGHIPQ $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)
  $}

  ${
    $d A x y $.  $d G x y z $.  $d H x y z $.  $d R y $.  $d X x y z $.
    ringid.1 $e |- G = ( 1st ` R ) $.
    ringid.2 $e |- H = ( 2nd ` R ) $.
    ringid.3 $e |- X = ran G $.
    $( The multiplication operation of a unital ring has (one or more) identity
       elements.  (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringid $p |- ( ( R e. Ring /\ A e. X ) ->
                 E. x e. X ( ( A H x ) = A /\ ( x H A ) = A ) ) $=
      ( vy vz wcel cring cv co wceq wa wrex wi opreq1 id eqeq12d opreq2
      anbi12d rexbidv imbi2d wral w3a cabl cxp wf ringi pm3.27d r19.12 ra4
      3syl com12 vtoclga impcom ) BFLCMLZBANZEOZBPZVABEOZBPZQZAFRZUTJNZVAEOZVHP
      ZVAVHEOZVHPZQZAFRZSUTVGSJBFVHBPZVNVGUTVOVMVFAFVOVJVCVLVEVOVIVBVHBVHBVAETV
      OUAZUBVOVKVDVHBVHBVAEUCVPUBUDUEUFUTVHFLZVNUTVMJFUGAFRZVNJFUGVQVNSUTVKKNZE
      OVAVHVSEOZEOPVAVHVSDOEOVKVAVSEOZDOPVAVHDOVSEOWAVTDOPUHKFUGJFUGAFUGZVRUTDU
      ILFFUJFEUKQWBVRQAJKCDEFGHIULUMUMVMAJFFUNVNJFUOUPUQURUS $.
      $( [14-Sep-2007] $) $( [8-Sep-2007] $)
  $}

  ${
    $d A x y z $.  $d B y z $.  $d C z $.  $d G x y z $.  $d H x y z $.
    $d X x y z $.
    ringdi.1 $e |- G = ( 1st ` R ) $.
    ringdi.2 $e |- H = ( 2nd ` R ) $.
    ringdi.3 $e |- X = ran G $.
    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringdi $p   |- ( ( R e. Ring /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                     ( A H ( B G C ) ) = ( ( A H B ) G ( A H C ) ) ) $=
      ( vx vy vz wcel w3a cring co wceq cv wral opreq1 opreq12d eqeq12d
      opreq2d opreq2 opreq1d rcla43v 3simp2 r19.20si syl5 wa wrex cabl cxp wf
      ringi pm3.27d pm3.26d impcom ) AGNBGNCGNOZDPNZABCEQZFQZABFQZACFQZEQZRZUTK
      SZLSZFQZMSZFQVHVIVKFQZFQRZVHVIVKEQZFQZVJVHVKFQZEQZRZVHVIEQVKFQVPVLEQRZOZM
      GTZLGTZKGTZVGVAUTVRMGTZLGTZKGTVGWCVRVGAVNFQZAVIFQZAVKFQZEQZRABVKEQZFQZVDW
      HEQZRKLMABCGGGVHARZVOWFVQWIVHAVNFUAWMVJWGVPWHEVHAVIFUAVHAVKFUAUBUCVIBRZWF
      WKWIWLWNVNWJAFVIBVKEUAUDWNWGVDWHEVIBAFUEUFUCVKCRZWKVCWLVFWOWJVBAFVKCBEUEU
      DWOWHVEVDEVKCAFUEUDUCUGWBWEKGWAWDLGVTVRMGVMVRVSUHUIUIUIUJVAWCVIVHFQVIRVJV
      IRUKLGTKGULZVAEUMNGGUNGFUOUKWCWPUKKLMDEFGHIJUPUQURUJUS $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringdir $p   |- ( ( R e. Ring /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A G B ) H C ) = ( ( A H C ) G ( B H C ) ) ) $=
      ( vx vy vz wcel w3a cring co wceq cv wral opreq1 opreq1d eqeq12d opreq2
      opreq2d opreq12d rcla43v 3simp3 r19.20si syl5 wa wrex cabl cxp wf ringi
      pm3.27d pm3.26d impcom ) AGNBGNCGNOZDPNZABEQZCFQZACFQZBCFQZEQZRZUTKSZLSZF
      QZMSZFQVHVIVKFQZFQRZVHVIVKEQFQVJVHVKFQZEQRZVHVIEQZVKFQZVNVLEQZRZOZMGTZLGT
      ZKGTZVGVAUTVSMGTZLGTZKGTVGWCVSVGAVIEQZVKFQZAVKFQZVLEQZRVBVKFQZWHBVKFQZEQZ
      RKLMABCGGGVHARZVQWGVRWIWMVPWFVKFVHAVIEUAUBWMVNWHVLEVHAVKFUAUBUCVIBRZWGWJW
      IWLWNWFVBVKFVIBAEUDUBWNVLWKWHEVIBVKFUAUEUCVKCRZWJVCWLVFVKCVBFUDWOWHVDWKVE
      EVKCAFUDVKCBFUDUFUCUGWBWEKGWAWDLGVTVSMGVMVOVSUHUIUIUIUJVAWCVIVHFQVIRVJVIR
      UKLGTKGULZVAEUMNGGUNGFUOUKWCWPUKKLMDEFGHIJUPUQURUJUS $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( Associative law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringass $p   |- ( ( R e. Ring /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A H B ) H C ) = ( A H ( B H C ) ) ) $=
      ( vx vy vz wcel w3a cring co wceq cv wral opreq1 opreq1d eqeq12d opreq2
      opreq2d rcla43v wa wrex cabl cxp wf ringi pm3.27d pm3.26d 3simp1
      r19.20si syl syl5 impcom ) AGNBGNCGNOZDPNZABFQZCFQZABCFQZFQZRZUTKSZLSZFQZ
      MSZFQZVGVHVJFQZFQZRZMGTZLGTZKGTZVFVAVNVFAVHFQZVJFQZAVLFQZRVBVJFQZABVJFQZF
      QZRKLMABCGGGVGARZVKVSVMVTWDVIVRVJFVGAVHFUAUBVGAVLFUAUCVHBRZVSWAVTWCWEVRVB
      VJFVHBAFUDUBWEVLWBAFVHBVJFUAUEUCVJCRZWAVCWCVEVJCVBFUDWFWBVDAFVJCBFUDUEUCU
      FVAVNVGVHVJEQFQVIVGVJFQZEQRZVGVHEQVJFQWGVLEQRZOZMGTZLGTZKGTZVQVAWMVHVGFQV
      HRVIVHRUGLGTKGUHZVAEUINGGUJGFUKUGWMWNUGKLMDEFGHIJULUMUNWLVPKGWKVOLGWJVNMG
      VNWHWIUOUPUPUPUQURUS $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)
  $}

  ${
    $d A x $.  $d G x $.  $d H x $.  $d R x $.  $d X x $.
    ring2.1 $e |- G = ( 1st ` R ) $.
    ring2.2 $e |- H = ( 2nd ` R ) $.
    ring2.3 $e |- X = ran G $.
    $( A ring element plus itself is two times the element.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ring2 $p   |- ( ( R e. Ring /\ A e. X ) ->
                  E. x e. X ( A G A ) = ( ( x G x ) H A ) ) $=
      ( cring wcel wa cv co wceq wrex ringid pm3.27 r19.22si opreq12 anidms
      3syl wi w3a eqtrt eqcomd ringdir expcom 3expia 3imp 3com13 sylan ex
      3expa r19.22dva mpd ) CJKZBFKZLZAMZBENZVADNZBBDNZOZAFPZVCUTUTDNBENZOZAFPU
      SBUTENBOZVABOZLZAFPVIAFPVEABCDEFGHIQVJVIAFVHVIRSVIVDAFVIVDVABVABDTUASUBUS
      VDVGAFUQURUTFKZVDVGUCUQURVKUDZVDVGVFVBOZVDVGVLVMVDLVFVCVFVBVCUEUFVKURUQVM
      VKURUQVMVKURUQVMUCZUCVKVKURVNUQVKVKURUDVMUTUTBCDEFGHIUGUHUIUAUJUKULUMUNUO
      UP $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)
  $}

  ${
    $d G x y z $.  $d R x y z $.
    ringabl.1 $e |- G = ( 1st ` R ) $.
    $( A ring's addition operation is an Abelian group operation.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringabl $p   |- ( R e. Ring -> G e. Abel ) $=
      ( vx vy vz cring wcel cabl crn cxp c2nd cfv wf wa cv co wceq w3a wral
      wrex eqid ringi pm3.26d ) AGHZBIHZBJZUGKUGALMZNZUEUFUIODPZEPZUHQZFPZUHQUJ
      UKUMUHQZUHQRUJUKUMBQUHQULUJUMUHQZBQRUJUKBQUMUHQUOUNBQRSFUGTEUGTDUGTUKUJUH
      QUKRULUKROEUGTDUGUAODEFABUHUGCUHUBUGUBUCUDUD $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)
  $}

  ${
    ringgrp.1 $e |- G = ( 1st ` R ) $.
    $( A ring's addition operation is a group operation.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ringgrp $p   |- ( R e. Ring -> G e. Grp ) $=
      ( cring wcel cabl cgr ringabl ablgrp syl ) ADEBFEBGEABCHBIJ $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)
  $}

  ${
    ringgcl.1 $e |- G = ( 1st ` R ) $.
    ringgcl.2 $e |- X = ran G $.
    $( Closure law for the addition (group) operation of a ring.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringgcl $p   |- ( ( R e. Ring /\ A e. X /\ B e. X ) -> ( A G B ) e. X ) $=
      ( cgr wcel co cring grpcl ringgrp syl3an1 ) DHIAEIBEIABDJEICKIABDEGLCDFMN
      $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( The addition operation of a ring is commutative.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ringcom $p   |- ( ( R e. Ring /\ A e. X /\ B e. X ) ->
                      ( A G B ) = ( B G A ) ) $=
      ( cabl wcel co wceq cring ablcom ringabl syl3an1 ) DHIAEIBEIABDJBADJKCLIA
      BDEGMCDFNO $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( The addition operation of a ring is associative.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ringaass $p   |- ( ( R e. Ring /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                        ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( cgr wcel w3a co wceq cring grpass ringgrp sylan ) EIJAFJBFJCFJKABELCELA
      BCELELMDNJABCEFHODEGPQ $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( The addition operation of a ring is commutative.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ringa23 $p   |- ( ( R e. Ring /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cabl wcel w3a co wceq cring abl23 ringabl sylan ) EIJAFJBFJCFJKABELCELA
      CELBELMDNJABCEFHODEGPQ $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( Rearrangement of 4 terms in a sum of ring elements.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ringa4 $p   |- ( ( R e. Ring /\ ( A e. X /\ B e. X ) /\
                   ( C e. X /\ D e. X ) ) ->
                     ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( cabl wcel wa co wceq cring abl4 ringabl syl3an1 ) FJKAGKBGKLCGKDGKLABFM
      CDFMFMACFMBDFMFMNEOKABCDFGIPEFHQR $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( Right cancellation law for the addition operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringrcan $p   |- ( ( R e. Ring /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                       ( ( A G C ) = ( B G C ) <-> A = B ) ) $=
      ( cgr wcel w3a co wceq wb cring grprcan ringgrp sylan ) EIJAFJBFJCFJKACEL
      BCELMABMNDOJABCEFHPDEGQR $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( Left cancellation law for the addition operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringlcan $p   |- ( ( R e. Ring /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                       ( ( C G A ) = ( C G B ) <-> A = B ) ) $=
      ( cgr wcel w3a co wceq wb cring grplcan ringgrp sylan ) EIJAFJBFJCFJKCAEL
      CBELMABMNDOJABCEFHPDEGQR $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)
  $}

  ${
    ring0cl.1 $e |- G = ( 1st ` R ) $.
    ring0cl.2 $e |- X = ran G $.
    ring0cl.3 $e |- Z = ( Id ` G ) $.
    $( A ring has an additive identity element.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ring0cl $p   |- ( R e. Ring -> Z e. X ) $=
      ( cring wcel cgr ringgrp grpidcl syl ) AHIBJIDCIABEKDBCFGLM $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( The additive identity of a ring is a right identity element.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ring0rid $p   |- ( ( R e. Ring /\ A e. X ) -> ( A G Z ) = A ) $=
      ( cgr wcel co wceq cring grprid ringgrp sylan ) CIJADJAECKALBMJAECDGHNBCF
      OP $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)

    $( The additive identity of a ring is a left identity element.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ring0lid $p   |- ( ( R e. Ring /\ A e. X ) -> ( Z G A ) = A ) $=
      ( cgr wcel co wceq cring grplid ringgrp sylan ) CIJADJEACKALBMJAECDGHNBCF
      OP $.
      $( [14-Sep-2007] $) $( [9-Sep-2007] $)
  $}

