$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c RingOps $.

  $( Extend class notation with the class of all unital rings. $)
  cring $a class RingOps $.

  ${
    $d g h x y z $.
    $( Define the class of all unital rings. $)
    df-ringo $a |- RingOps = { <. g , h >. | ( ( g e. AbelOp /\
                               h : ( ran g X. ran g ) --> ran g ) /\
            ( A. x e. ran g A. y e. ran g A. z e. ran g
              ( ( ( x h y ) h z ) = ( x h ( y h z ) ) /\
                ( x h ( y g z ) ) = ( ( x h y ) g ( x h z ) ) /\
                ( ( x g y ) h z ) = ( ( x h z ) g ( y h z ) ) ) /\
        E. x e. ran g A. y e. ran g ( ( x h y ) = y /\ ( y h x ) = y ) ) ) } $.
  $}

  ${
    $d g h x y z $.
    $( The class of all unital rings is a relation.  (Contributed by FL,
       31-Aug-2009.)  (Revised by Mario Carneiro, 21-Dec-2013.) $)
    relrng $p |- Rel RingOps $=
      ( vg vh vx vy vz cv cabl wcel crn cxp wf wa wceq wral wrex cring df-ringo
      co w3a relopabi ) AFZGHUAIZUBJUBBFZKLCFZDFZUCRZEFZUCRUDUEUGUCRZUCRMUDUEUG
      UARUCRUFUDUGUCRZUARMUDUEUARUGUCRUIUHUARMSEUBNDUBNCUBNUFUEMUEUDUCRUEMLDUBN
      CUBOLLABPCDEABQT $.
      $( [21-Dec-2013] $) $( [31-Aug-2009] $)
  $}

  ${
    $d g h x y z G $.  $d g h x y z H $.  $d g h x y z X $.
    isring.1 $e |- X = ran G $.
    $( The predicate "is a (unital) ring."  Definition of ring with unit in
       [Schechter] p. 187.  (Contributed by Jeffrey Hankins, 21-Nov-2006.)
       (Revised by Mario Carneiro, 21-Dec-2013.) $)
    isringo $p |- ( H e. A -> ( <. G , H >. e. RingOps <->
      ( ( G e. AbelOp /\ H : ( X X. X ) --> X ) /\
        ( A. x e. X A. y e. X A. z e. X
          ( ( ( x H y ) H z ) = ( x H ( y H z ) ) /\
            ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) /\
            ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) /\
          E. x e. X A. y e. X ( ( x H y ) = y /\ ( y H x ) = y ) ) ) ) ) $=
      ( vg vh wcel cring cabl wa cv co wceq wral opreqd opreq123d cvv cop wf wi
      cxp w3a wrex wbr df-br relrng brrelexi sylbir a1i elex ad2antrr crn copab
      wb df-ringo eleq2i simpl eleq1d simpr feq1d rneqd syl6eqr xpeq12d anbi12d
      feq23d bitrd eqidd 3anbi123d raleqbidv eqeq1d rexeqbidv opelopabga syl5bb
      eqeq12d expcom pm5.21ndd ) FDKZEUAKZEFUBZLKZEMKZGGUEZGFUCZNZAOZBOZFPZCOZF
      PZWIWJWLFPZFPZQZWIWJWLEPZFPZWKWIWLFPZEPZQZWIWJEPZWLFPZWSWNEPZQZUFZCGRZBGR
      ZAGRZWKWJQZWJWIFPZWJQZNZBGRZAGUGZNZNZWDWBUDWAWDEFLUHWBEFLUIEFLUJUKULUMXQW
      BUDWAWEWBWGXPEMUNUOUMWBWAWDXQURWDWCIOZMKZXRUPZXTUEZXTJOZUCZNZWIWJYBPZWLYB
      PZWIWJWLYBPZYBPZQZWIWJWLXRPZYBPZYEWIWLYBPZXRPZQZWIWJXRPZWLYBPZYLYGXRPZQZU
      FZCXTRZBXTRZAXTRZYEWJQZWJWIYBPZWJQZNZBXTRZAXTUGZNZNZIJUQZKWBWANXQLUUKWCAB
      CIJUSUTUUJXQIJEFUADXREQZYBFQZNZYDWHUUIXPUUNXSWEYCWGUUNXREMUULUUMVAZVBUUNY
      CYAXTFUCWGUUNYAXTYBFUULUUMVCZVDUUNYAXTWFGFUUNXTGXTGUUNXTEUPGUUNXREUUOVEHV
      FZUUQVGUUQVIVJVHUUNUUBXIUUHXOUUNUUAXHAXTGUUQUUNYTXGBXTGUUQUUNYSXFCXTGUUQU
      UNYIWPYNXAYRXEUUNYFWMYHWOUUNYEWKWLWLYBFUUPUUNYBFWIWJUUPSZUUNWLVKZTUUNWIWI
      YGWNYBFUUPUUNWIVKZUUNYBFWJWLUUPSZTVRUUNYKWRYMWTUUNWIWIYJWQYBFUUPUUTUUNXRE
      WJWLUUOSTUUNYEWKYLWSXREUUOUURUUNYBFWIWLUUPSZTVRUUNYPXCYQXDUUNYOXBWLWLYBFU
      UPUUNXREWIWJUUOSUUSTUUNYLWSYGWNXREUUOUVBUVATVRVLVMVMVMUUNUUGXNAXTGUUQUUNU
      UFXMBXTGUUQUUNUUCXJUUEXLUUNYEWKWJUURVNUUNUUDXKWJUUNYBFWJWIUUPSVNVHVMVOVHV
      HVPVQVSVT $.
      $( [21-Dec-2013] $) $( [21-Nov-2006] $)
  $}

  ${
    $d ph x y z $.  $d G x y z $.  $d H x y z $.  $d X x y z $.  $d U x y $.
    isringod.1 $e |- ( ph -> G e. AbelOp ) $.
    isringod.2 $e |- ( ph -> X = ran G ) $.
    isringod.3 $e |- ( ph -> H : ( X X. X ) --> X ) $.
    isringod.4 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                                  -> ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $.
    isringod.5 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $.
    isringod.6 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) $.
    isringod.7 $e |- ( ph -> U e. X ) $.
    isringod.8 $e |- ( ( ph /\ y e. X ) -> ( U H y ) = y ) $.
    isringod.9 $e |- ( ( ph /\ y e. X ) -> ( y H U ) = y ) $.
    $( Conditions that determine a ring.  (Changed label from ~ isrngd to
       ~ isringod -NM 2-Aug-2013.)  (Contributed by Jeff Madsen, 19-Jun-2010.)
       (Revised by Mario Carneiro, 21-Dec-2013.) $)
    isringod $p |- ( ph -> <. G , H >. e. RingOps ) $=
      ( wcel wa co cop cring cabl crn cxp wf cv wceq w3a wral wrex feq23d mpbid
      xpeq12d wi ex 3expd imp32 ralrimiv ralrimivva raleqdv raleqbidv ralrimiva
      3jca opreq1 eqeq1d opreq2 anbi12d ralbidv rcla4ev syl2anc rexeqbidv jca31
      jca cvv wb rnexg syl xpexg fex eqid isringo mpbird ) AFGUAUBRZFUCRZFUDZWF
      UEZWFGUFZSBUGZCUGZGTZDUGZGTWIWJWLGTZGTUHZWIWJWLFTGTWKWIWLGTZFTUHZWIWJFTWL
      GTWOWMFTUHZUIZDWFUJZCWFUJZBWFUJZWKWJUHZWJWIGTZWJUHZSZCWFUJZBWFUKZSZSZAWEW
      HXHIAHHUEZHGUFWHKAXJHWGWFGAHWFHWFJJUNJULUMZAXAXGAWRDHUJZCHUJZBHUJXAAXLBCH
      HAWIHRZWJHRZSSWRDHAXNXOWLHRZWRUOAXNXOXPWRAXNXOXPUIZWRAXQSWNWPWQLMNVDUPUQU
      RUSUTAXMWTBHWFJAXLWSCHWFJAWRDHWFJVAVBVBUMAXECHUJZBHUKZXGAEHREWJGTZWJUHZWJ
      EGTZWJUHZSZCHUJZXSOAYDCHAXOSYAYCPQVNVCXRYEBEHWIEUHZXEYDCHYFXBYAXDYCYFWKXT
      WJWIEWJGVEVFYFXCYBWJWIEWJGVGVFVHVIVJVKAXRXFBHWFJAXECHWFJVAVLUMVNVMAGVORZW
      DXIVPAWHWGVORZYGXKAWFVORZYIYHAWEYIIFUCVQVRZYJWFWFVOVOVSVKWGWFVOGVTVKBCDVO
      FGWFWFWAWBVRWC $.
      $( [21-Dec-2013] $) $( [19-Jun-2010] $)
  $}

  ${
    $d u x y z G $.  $d u x y z H $.  $d u x y z X $.  $d u x y z A $.
    $d y z B $.  $d z C $.  $d u x R $.
    ringi.1 $e |- G = ( 1st ` R ) $.
    ringi.2 $e |- H = ( 2nd ` R ) $.
    ringi.3 $e |- X = ran G $.
    $( The properties of a unital ring.  (Contributed by Steve Rodriguez,
       8-Sep-2007.)  (The proof was shortened by Mario Carneiro,
       21-Dec-2013.) $)
    ringoi $p |- ( R e. RingOps -> ( ( G e. AbelOp /\ H : ( X X. X ) --> X ) /\
      ( A. x e. X A. y e. X A. z e. X
        ( ( ( x H y ) H z ) = ( x H ( y H z ) ) /\
          ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) /\
          ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) /\
        E. x e. X A. y e. X ( ( x H y ) = y /\ ( y H x ) = y ) ) ) ) $=
      ( cring wcel cop wa cv co wceq wral cfv cvv cabl cxp w3a wrex c1st relrng
      wf c2nd wrel 1st2nd mpan opeq12i syl6reqr id eqeltrd fvex eqeltri isringo
      wb ax-mp sylib ) DKLZEFMZKLZEUALGGUBGFUGNAOZBOZFPZCOZFPVEVFVHFPZFPQVEVFVH
      EPFPVGVEVHFPZEPQVEVFEPVHFPVJVIEPQUCCGRBGRAGRVGVFQVFVEFPVFQNBGRAGUDNNZVBVC
      DKVBDDUESZDUHSZMZVCKUIVBDVNQUFDKUJUKEVLFVMHIULUMVBUNUOFTLVDVKUSFVMTIDUHUP
      UQABCTEFGJURUTVA $.
      $( [21-Dec-2013] $) $( [8-Sep-2007] $)

    $( Functionality of the multiplication operation of a ring.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringsm $p |- ( R e. RingOps -> H : ( X X. X ) --> X ) $=
      ( vx vy vz cring wcel cabl cxp wf wa cv co wceq wral ringoi simpld simprd
      w3a wrex ) AKLZBMLZDDNDCOZUFUGUHPHQZIQZCRZJQZCRUIUJULCRZCRSUIUJULBRCRUKUI
      ULCRZBRSUIUJBRULCRUNUMBRSUDJDTIDTHDTUKUJSUJUICRUJSPIDTHDUEPHIJABCDEFGUAUB
      UC $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)

    $( Closure of the multiplication operation of a ring.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ringocl $p |- ( ( R e. RingOps /\ A e. X /\ B e. X ) -> ( A H B ) e. X ) $=
      ( cring wcel cxp wf co ringsm foprrn syl3an1 ) CJKFFLFEMAFKBFKABENFKCDEFG
      HIOABFFFEPQ $.
      $( [9-Sep-2007] $)

    $( The multiplication operation of a unital ring has (one or more) identity
       elements.  (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringid $p |- ( ( R e. RingOps /\ A e. X ) ->
                 E. u e. X ( ( u H A ) = A /\ ( A H u ) = A ) ) $=
      ( vx vy wcel cv co wceq wa wrex wral simprd eqeq12d cring w3a cabl cxp wf
      ringoi r19.12 syl opreq2 id opreq1 anbi12d rexbidv rcla4cva sylan ) CUALZ
      AMZJMZENZUROZURUQENZUROZPZAFQZJFRZBFLUQBENZBOZBUQENZBOZPZAFQZUPVCJFRAFQZV
      EUPUSKMZENUQURVMENZENOUQURVMDNENUSUQVMENZDNOUQURDNVMENVOVNDNOUBKFRJFRAFRZ
      VLUPDUCLFFUDFEUEPVPVLPAJKCDEFGHIUFSSVCAJFFUGUHVDVKJBFURBOZVCVJAFVQUTVGVBV
      IVQUSVFURBURBUQEUIVQUJZTVQVAVHURBURBUQEUKVRTULUMUNUO $.
      $( [22-Dec-2013] $) $( [8-Sep-2007] $)

    $( The unit element of a ring is unique. $)
    ringoideu $p |- ( R e. RingOps ->
                 E! u e. X A. x e. X ( ( u H x ) = x /\ ( x H u ) = x ) ) $=
      ( vy wcel cv co wceq wa wral ralimi opreq2 id eqeq12d cring wrex wreu cxp
      wi cabl wf w3a ringoi simprr syl simpl rcla4v syl5 simpr opreq1 im2anan9r
      eqtr2 eqcomd syl6 rgen2 jctir eqeq1d anbi12d ralbidv reu4 sylibr ) CUAKZB
      LZALZEMZVJNZVJVIEMZVJNZOZAFPZBFUBZVPJLZVJEMZVJNZVJVREMZVJNZOZAFPZOZVIVRNZ
      UEZJFPBFPZOVPBFUCVHVQWHVHDUFKFFUDFEUGOZVKVREMVIWAEMNVIVJVRDMEMVKVIVREMZDM
      NVIVJDMVREMWJWADMNUHJFPAFPBFPZVQOOVQBAJCDEFGHIUIWIWKVQUJUKWGBJFFVIFKZVRFK
      ZOWEWJVRNZWJVINZOZWFWMVPWNWLWDWOVPVLAFPWMWNVOVLAFVLVNULQVLWNAVRFVJVRNZVKW
      JVJVRVJVRVIERWQSTUMUNWDWBAFPWLWOWCWBAFVTWBUOQWBWOAVIFVJVINZWAWJVJVIVJVIVR
      EUPWRSTUMUNUQWPVRVIWJVRVIURUSUTVAVBVPWDBJFWFVOWCAFWFVLVTVNWBWFVKVSVJVIVRV
      JEUPVCWFVMWAVJVIVRVJERVCVDVEVFVG $.
      $( [21-Dec-2013] $) $( [4-Apr-2009] $)

    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringodi $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                     ( A H ( B G C ) ) = ( ( A H B ) G ( A H C ) ) ) $=
      ( vx vy vz wcel co wceq cv wral wa opreq1 w3a wrex cabl cxp ringoi simprd
      cring simpld simp2 ralimi opreq12d eqeq12d opreq2d opreq2 opreq1d rcla43v
      wf syl5 impcom ) AGNBGNCGNUAZDUGNZABCEOZFOZABFOZACFOZEOZPZVAKQZLQZFOZMQZF
      OVHVIVKFOZFOPZVHVIVKEOZFOZVJVHVKFOZEOZPZVHVIEOVKFOVPVLEOPZUAZMGRZLGRZKGRZ
      UTVGVAWCVJVIPVIVHFOVIPSLGRKGUBZVAEUCNGGUDGFUQSWCWDSKLMDEFGHIJUEUFUHWCVRMG
      RZLGRZKGRUTVGWBWFKGWAWELGVTVRMGVMVRVSUIUJUJUJVRVGAVNFOZAVIFOZAVKFOZEOZPAB
      VKEOZFOZVDWIEOZPKLMABCGGGVHAPZVOWGVQWJVHAVNFTWNVJWHVPWIEVHAVIFTVHAVKFTUKU
      LVIBPZWGWLWJWMWOVNWKAFVIBVKETUMWOWHVDWIEVIBAFUNUOULVKCPZWLVCWMVFWPWKVBAFV
      KCBEUNUMWPWIVEVDEVKCAFUNUMULUPURURUS $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)

    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringodir $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A G B ) H C ) = ( ( A H C ) G ( B H C ) ) ) $=
      ( vx vy vz wcel co wceq cv wral wa opreq2 w3a wrex cabl cxp ringoi simprd
      cring simpld simp3 ralimi opreq1 opreq1d eqeq12d opreq2d opreq12d rcla43v
      wf syl5 impcom ) AGNBGNCGNUAZDUGNZABEOZCFOZACFOZBCFOZEOZPZVAKQZLQZFOZMQZF
      OVHVIVKFOZFOPZVHVIVKEOFOVJVHVKFOZEOPZVHVIEOZVKFOZVNVLEOZPZUAZMGRZLGRZKGRZ
      UTVGVAWCVJVIPVIVHFOVIPSLGRKGUBZVAEUCNGGUDGFUQSWCWDSKLMDEFGHIJUEUFUHWCVSMG
      RZLGRZKGRUTVGWBWFKGWAWELGVTVSMGVMVOVSUIUJUJUJVSVGAVIEOZVKFOZAVKFOZVLEOZPV
      BVKFOZWIBVKFOZEOZPKLMABCGGGVHAPZVQWHVRWJWNVPWGVKFVHAVIEUKULWNVNWIVLEVHAVK
      FUKULUMVIBPZWHWKWJWMWOWGVBVKFVIBAETULWOVLWLWIEVIBVKFUKUNUMVKCPZWKVCWMVFVK
      CVBFTWPWIVDWLVEEVKCAFTVKCBFTUOUMUPURURUS $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)

    $( Associative law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringoass $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A H B ) H C ) = ( A H ( B H C ) ) ) $=
      ( vx vy vz wcel co wceq cv wral wa ralimi w3a wrex cabl cxp ringoi simprd
      cring simpld simp1 syl opreq1 opreq1d eqeq12d opreq2 opreq2d rcla43v syl5
      wf impcom ) AGNBGNCGNUAZDUGNZABFOZCFOZABCFOZFOZPZVAKQZLQZFOZMQZFOZVGVHVJF
      OZFOZPZMGRZLGRZKGRZUTVFVAVNVGVHVJEOFOVIVGVJFOZEOPZVGVHEOVJFOVRVLEOPZUAZMG
      RZLGRZKGRZVQVAWDVIVHPVHVGFOVHPSLGRKGUBZVAEUCNGGUDGFURSWDWESKLMDEFGHIJUEUF
      UHWCVPKGWBVOLGWAVNMGVNVSVTUITTTUJVNVFAVHFOZVJFOZAVLFOZPVBVJFOZABVJFOZFOZP
      KLMABCGGGVGAPZVKWGVMWHWLVIWFVJFVGAVHFUKULVGAVLFUKUMVHBPZWGWIWHWKWMWFVBVJF
      VHBAFUNULWMVLWJAFVHBVJFUKUOUMVJCPZWIVCWKVEVJCVBFUNWNWJVDAFVJCBFUNUOUMUPUQ
      US $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)

    $( A ring element plus itself is two times the element.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ring2 $p |- ( ( R e. RingOps /\ A e. X ) ->
                  E. x e. X ( A G A ) = ( ( x G x ) H A ) ) $=
      ( cring wcel wa cv co wceq wrex ringid opreq12 anidms eqcomd simpll simpr
      simplr ringodir syl13anc eqeq2d syl5ibr adantrd reximdva mpd ) CJKZBFKZLZ
      AMZBENZBOZBUNENBOZLZAFPBBDNZUNUNDNBENZOZAFPABCDEFGHIQUMURVAAFUMUNFKZLZUPV
      AUQUPVAVCUSUOUODNZOUPVDUSUPVDUSOUOBUOBDRSTVCUTVDUSVCUKVBVBULUTVDOUKULVBUA
      UMVBUBZVEUKULVBUCUNUNBCDEFGHIUDUEUFUGUHUIUJ $.
      $( [22-Dec-2013] $) $( [9-Sep-2007] $)
  $}

  ${
    $d G x y z $.  $d R x y z $.
    ringabl.1 $e |- G = ( 1st ` R ) $.
    $( A ring's addition operation is an Abelian group operation.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringoablo $p |- ( R e. RingOps -> G e. AbelOp ) $=
      ( vx vy vz cring wcel cabl crn cxp c2nd cfv wa cv wceq wral eqid simpld
      co wf w3a wrex ringoi ) AGHZBIHZBJZUGKUGALMZUAZUEUFUINDOZEOZUHTZFOZUHTUJU
      KUMUHTZUHTPUJUKUMBTUHTULUJUMUHTZBTPUJUKBTUMUHTUOUNBTPUBFUGQEUGQDUGQULUKPU
      KUJUHTUKPNEUGQDUGUCNDEFABUHUGCUHRUGRUDSS $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)
  $}

  ${
    ringgrp.1 $e |- G = ( 1st ` R ) $.
    $( A ring's addition operation is a group operation.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ringogrpo $p |- ( R e. RingOps -> G e. GrpOp ) $=
      ( cring wcel cabl cgr ringoablo ablogrpo syl ) ADEBFEBGEABCHBIJ $.
      $( [9-Sep-2007] $)
  $}

  ${
    ringgcl.1 $e |- G = ( 1st ` R ) $.
    ringgcl.2 $e |- X = ran G $.
    $( Closure law for the addition (group) operation of a ring.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    ringgcl $p |- ( ( R e. RingOps /\ A e. X /\ B e. X ) -> ( A G B ) e. X ) $=
      ( cring wcel cgr co ringogrpo grpocl syl3an1 ) CHIDJIAEIBEIABDKEICDFLABDE
      GMN $.
      $( [9-Sep-2007] $)

    $( The addition operation of a ring is commutative.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ringcom $p |- ( ( R e. RingOps /\ A e. X /\ B e. X ) ->
                      ( A G B ) = ( B G A ) ) $=
      ( cring wcel cabl co wceq ringoablo ablocom syl3an1 ) CHIDJIAEIBEIABDKBAD
      KLCDFMABDEGNO $.
      $( [9-Sep-2007] $)

    $( The addition operation of a ring is associative.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ringaass $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                        ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( cring wcel cgr w3a co wceq ringogrpo grpoass sylan ) DIJEKJAFJBFJCFJLAB
      EMCEMABCEMEMNDEGOABCEFHPQ $.
      $( [9-Sep-2007] $)

    $( The addition operation of a ring is commutative.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ringa32 $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cring wcel cabl w3a co wceq ringoablo ablo32 sylan ) DIJEKJAFJBFJCFJLAB
      EMCEMACEMBEMNDEGOABCEFHPQ $.
      $( [9-Sep-2007] $)

    $( Rearrangement of 4 terms in a sum of ring elements.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    ringa4 $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X ) /\
                   ( C e. X /\ D e. X ) ) ->
                     ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( cring wcel cabl wa co wceq ringoablo ablo4 syl3an1 ) EJKFLKAGKBGKMCGKDG
      KMABFNCDFNFNACFNBDFNFNOEFHPABCDFGIQR $.
      $( [9-Sep-2007] $)

    $( Right cancellation law for the addition operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringrcan $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                       ( ( A G C ) = ( B G C ) <-> A = B ) ) $=
      ( cring wcel cgr w3a co wceq wb ringogrpo grporcan sylan ) DIJEKJAFJBFJCF
      JLACEMBCEMNABNODEGPABCEFHQR $.
      $( [9-Sep-2007] $)

    $( Left cancellation law for the addition operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringlcan $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                       ( ( C G A ) = ( C G B ) <-> A = B ) ) $=
      ( cring wcel cgr w3a co wceq wb ringogrpo grpolcan sylan ) DIJEKJAFJBFJCF
      JLCAEMCBEMNABNODEGPABCEFHQR $.
      $( [9-Sep-2007] $)
  $}

  ${
    ring0cl.1 $e |- G = ( 1st ` R ) $.
    ring0cl.2 $e |- X = ran G $.
    ring0cl.3 $e |- Z = ( Id ` G ) $.
    $( A ring has an additive identity element.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    ring0cl $p |- ( R e. RingOps -> Z e. X ) $=
      ( cring wcel cgr ringogrpo grpoidcl syl ) AHIBJIDCIABEKDBCFGLM $.
      $( [9-Sep-2007] $)

    $( The additive identity of a ring is a right identity element.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ring0rid $p |- ( ( R e. RingOps /\ A e. X ) -> ( A G Z ) = A ) $=
      ( cring wcel cgr co wceq ringogrpo grporid sylan ) BIJCKJADJAECLAMBCFNAEC
      DGHOP $.
      $( [9-Sep-2007] $)

    $( The additive identity of a ring is a left identity element.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ring0lid $p |- ( ( R e. RingOps /\ A e. X ) -> ( Z G A ) = A ) $=
      ( cring wcel cgr co wceq ringogrpo grpolid sylan ) BIJCKJADJEACLAMBCFNAEC
      DGHOP $.
      $( [9-Sep-2007] $)
  $}

  ${
    ringlz.1 $e |- Z = ( Id ` G ) $.
    ringlz.2 $e |- X = ran G $.
    ringlz.3 $e |- G = ( 1st ` R ) $.
    ringlz.4 $e |- H = ( 2nd ` R ) $.
    $( The zero of a unital ring is a left absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    ringolz $p |- ( ( R e. RingOps /\ A e. X ) -> ( Z H A ) = Z ) $=
      ( cring wcel wa co wceq cgr ringogrpo grpoidcl grpolid adantr syl opreq1d
      mpdan w3a ring0cl simpr 3jca ringodir syldan simpl ringocl syl3anc eqcomd
      grporid syl2anc 3eqtr3d wb grpolcan syl13anc mpbid ) BKLZAELZMZFADNZVDCNZ
      VDFCNZOZVDFOZVCFFCNZADNZVDVEVFVCVIFADVAVIFOZVBVACPLZVKBCIQZVLFELZVKFCEHGR
      FFCEHGSUCUATUBVAVBVNVNVBUDVJVEOVCVNVNVBVAVNVBBCEFIHGUETZVOVAVBUFZUGFFABCD
      EIJHUHUIVCVLVDELZVDVFOVAVLVBVMTZVCVAVNVBVQVAVBUJVOVPFABCDEIJHUKULZVLVQMVF
      VDVDFCEHGUNUMUOUPVCVLVQVNVQVGVHUQVRVSVOVSVDFVDCEHURUSUT $.
      $( [31-Aug-2009] $)

    $( The zero of a unital ring is a right absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    ringorz $p |- ( ( R e. RingOps /\ A e. X ) -> ( A H Z ) = Z ) $=
      ( cring wcel wa co wceq cgr ringogrpo grpoidcl grpolid adantr syl opreq2d
      mpdan simpr ring0cl ringodi syldan ringocl mpd3an3 eqcomd syl2anc 3eqtr3d
      w3a 3jca wb grporcan syl13anc mpbid ) BKLZAELZMZAFDNZVBCNZFVBCNZOZVBFOZVA
      AFFCNZDNZVBVCVDVAVGFADUSVGFOZUTUSCPLZVIBCIQZVJFELZVIFCEHGRFFCEHGSUCUATUBU
      SUTUTVLVLUMVHVCOVAUTVLVLUSUTUDUSVLUTBCEFIHGUETZVMUNAFFBCDEIJHUFUGVAVJVBEL
      ZVBVDOUSVJUTVKTZUSUTVLVNVMAFBCDEIJHUHUIZVJVNMVDVBVBFCEHGSUJUKULVAVJVNVLVN
      VEVFUOVOVPVMVPVBFVBCEHUPUQUR $.
      $( [31-Aug-2009] $)
  $}

