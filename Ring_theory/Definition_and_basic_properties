$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c RingOps $.

  $( Extend class notation with the class of all unital rings. $)
  crngo $a class RingOps $.

  ${
    $d g h x y z $.
    $( Define the class of all unital rings. $)
    df-rngo $a |- RingOps = { <. g , h >. | ( ( g e. AbelOp /\
                               h : ( ran g X. ran g ) --> ran g ) /\
            ( A. x e. ran g A. y e. ran g A. z e. ran g
              ( ( ( x h y ) h z ) = ( x h ( y h z ) ) /\
                ( x h ( y g z ) ) = ( ( x h y ) g ( x h z ) ) /\
                ( ( x g y ) h z ) = ( ( x h z ) g ( y h z ) ) ) /\
        E. x e. ran g A. y e. ran g ( ( x h y ) = y /\ ( y h x ) = y ) ) ) } $.
  $}

  ${
    $d g h x y z $.
    $( The class of all unital rings is a relation.  (Contributed by FL,
       31-Aug-2009.)  (Revised by Mario Carneiro, 21-Dec-2013.) $)
    relrngo $p |- Rel RingOps $=
      ( vg vh vx vy vz cv cabl wcel crn cxp wf wa co wceq w3a wral wrex df-rngo
      crngo relopabi ) AFZGHUAIZUBJUBBFZKLCFZDFZUCMZEFZUCMUDUEUGUCMZUCMNUDUEUGU
      AMUCMUFUDUGUCMZUAMNUDUEUAMUGUCMUIUHUAMNOEUBPDUBPCUBPUFUENUEUDUCMUENLDUBPC
      UBQLLABSCDEABRT $.
      $( [21-Dec-2013] $) $( [31-Aug-2009] $)
  $}

  ${
    $d g h x y z G $.  $d g h x y z H $.  $d g h x y z X $.
    isring.1 $e |- X = ran G $.
    $( The predicate "is a (unital) ring."  Definition of ring with unit in
       [Schechter] p. 187.  (Contributed by Jeffrey Hankins, 21-Nov-2006.)
       (Revised by Mario Carneiro, 21-Dec-2013.) $)
    isrngo $p |- ( H e. A -> ( <. G , H >. e. RingOps <->
      ( ( G e. AbelOp /\ H : ( X X. X ) --> X ) /\
        ( A. x e. X A. y e. X A. z e. X
          ( ( ( x H y ) H z ) = ( x H ( y H z ) ) /\
            ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) /\
            ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) /\
          E. x e. X A. y e. X ( ( x H y ) = y /\ ( y H x ) = y ) ) ) ) ) $=
      ( vg vh wcel crngo cabl wa cv co wceq wral oveqd oveq123d cvv cop cxp w3a
      wf wrex wi wbr df-br relrngo brrelexi sylbir a1i elex ad2antrr wb df-rngo
      copab eleq2i simpl eleq1d simpr feq1d rneqd syl6eqr xpeq12d bitrd anbi12d
      crn feq23d eqeq12d 3anbi123d raleqbidv eqeq1d rexeqbidv opelopabga syl5bb
      eqidd expcom pm5.21ndd ) FDKZEUAKZEFUBZLKZEMKZGGUCZGFUEZNZAOZBOZFPZCOZFPZ
      WIWJWLFPZFPZQZWIWJWLEPZFPZWKWIWLFPZEPZQZWIWJEPZWLFPZWSWNEPZQZUDZCGRZBGRZA
      GRZWKWJQZWJWIFPZWJQZNZBGRZAGUFZNZNZWDWBUGWAWDEFLUHWBEFLUIEFLUJUKULUMXQWBU
      GWAWEWBWGXPEMUNUOUMWBWAWDXQUPWDWCIOZMKZXRVIZXTUCZXTJOZUEZNZWIWJYBPZWLYBPZ
      WIWJWLYBPZYBPZQZWIWJWLXRPZYBPZYEWIWLYBPZXRPZQZWIWJXRPZWLYBPZYLYGXRPZQZUDZ
      CXTRZBXTRZAXTRZYEWJQZWJWIYBPZWJQZNZBXTRZAXTUFZNZNZIJURZKWBWANXQLUUKWCABCI
      JUQUSUUJXQIJEFUADXREQZYBFQZNZYDWHUUIXPUUNXSWEYCWGUUNXREMUULUUMUTZVAUUNYCY
      AXTFUEWGUUNYAXTYBFUULUUMVBZVCUUNYAXTWFGFUUNXTGXTGUUNXTEVIGUUNXREUUOVDHVEZ
      UUQVFUUQVJVGVHUUNUUBXIUUHXOUUNUUAXHAXTGUUQUUNYTXGBXTGUUQUUNYSXFCXTGUUQUUN
      YIWPYNXAYRXEUUNYFWMYHWOUUNYEWKWLWLYBFUUPUUNYBFWIWJUUPSZUUNWLVRZTUUNWIWIYG
      WNYBFUUPUUNWIVRZUUNYBFWJWLUUPSZTVKUUNYKWRYMWTUUNWIWIYJWQYBFUUPUUTUUNXREWJ
      WLUUOSTUUNYEWKYLWSXREUUOUURUUNYBFWIWLUUPSZTVKUUNYPXCYQXDUUNYOXBWLWLYBFUUP
      UUNXREWIWJUUOSUUSTUUNYLWSYGWNXREUUOUVBUVATVKVLVMVMVMUUNUUGXNAXTGUUQUUNUUF
      XMBXTGUUQUUNUUCXJUUEXLUUNYEWKWJUURVNUUNUUDXKWJUUNYBFWJWIUUPSVNVHVMVOVHVHV
      PVQVSVT $.
      $( [21-Dec-2013] $) $( [21-Nov-2006] $)
  $}

  ${
    $d ph x y z $.  $d G x y z $.  $d H x y z $.  $d X x y z $.  $d U x y $.
    isringod.1 $e |- ( ph -> G e. AbelOp ) $.
    isringod.2 $e |- ( ph -> X = ran G ) $.
    isringod.3 $e |- ( ph -> H : ( X X. X ) --> X ) $.
    isringod.4 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                                  -> ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $.
    isringod.5 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $.
    isringod.6 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) $.
    isringod.7 $e |- ( ph -> U e. X ) $.
    isringod.8 $e |- ( ( ph /\ y e. X ) -> ( U H y ) = y ) $.
    isringod.9 $e |- ( ( ph /\ y e. X ) -> ( y H U ) = y ) $.
    $( Conditions that determine a ring.  (Changed label from ~ isrngd to
       ~ isrngod -NM 2-Aug-2013.)  (Contributed by Jeff Madsen, 19-Jun-2010.)
       (Revised by Mario Carneiro, 21-Dec-2013.) $)
    isrngod $p |- ( ph -> <. G , H >. e. RingOps ) $=
      ( wcel wa co cop crngo cabl crn cxp wf cv wceq w3a wral wrex feq23d mpbid
      xpeq12d wi ex 3expd imp32 ralrimiv ralrimivva raleqdv raleqbidv ralrimiva
      3jca jca oveq1 eqeq1d oveq2 anbi12d ralbidv rcla4ev syl2anc rexeqbidv cvv
      jca31 wb rnexg syl xpexg fex eqid isrngo mpbird ) AFGUAUBRZFUCRZFUDZWFUEZ
      WFGUFZSBUGZCUGZGTZDUGZGTWIWJWLGTZGTUHZWIWJWLFTGTWKWIWLGTZFTUHZWIWJFTWLGTW
      OWMFTUHZUIZDWFUJZCWFUJZBWFUJZWKWJUHZWJWIGTZWJUHZSZCWFUJZBWFUKZSZSZAWEWHXH
      IAHHUEZHGUFWHKAXJHWGWFGAHWFHWFJJUNJULUMZAXAXGAWRDHUJZCHUJZBHUJXAAXLBCHHAW
      IHRZWJHRZSSWRDHAXNXOWLHRZWRUOAXNXOXPWRAXNXOXPUIZWRAXQSWNWPWQLMNVDUPUQURUS
      UTAXMWTBHWFJAXLWSCHWFJAWRDHWFJVAVBVBUMAXECHUJZBHUKZXGAEHREWJGTZWJUHZWJEGT
      ZWJUHZSZCHUJZXSOAYDCHAXOSYAYCPQVEVCXRYEBEHWIEUHZXEYDCHYFXBYAXDYCYFWKXTWJW
      IEWJGVFVGYFXCYBWJWIEWJGVHVGVIVJVKVLAXRXFBHWFJAXECHWFJVAVMUMVEVOAGVNRZWDXI
      VPAWHWGVNRZYGXKAWFVNRZYIYHAWEYIIFUCVQVRZYJWFWFVNVNVSVLWGWFVNGVTVLBCDVNFGW
      FWFWAWBVRWC $.
      $( [21-Dec-2013] $) $( [19-Jun-2010] $)
  $}

  ${
    $d u x y z G $.  $d u x y z H $.  $d u x y z X $.  $d u x y z A $.
    $d y z B $.  $d z C $.  $d u x R $.
    ringi.1 $e |- G = ( 1st ` R ) $.
    ringi.2 $e |- H = ( 2nd ` R ) $.
    ringi.3 $e |- X = ran G $.
    $( The properties of a unital ring.  (Contributed by Steve Rodriguez,
       8-Sep-2007.)  (The proof was shortened by Mario Carneiro,
       21-Dec-2013.) $)
    rngoi $p |- ( R e. RingOps -> ( ( G e. AbelOp /\ H : ( X X. X ) --> X ) /\
      ( A. x e. X A. y e. X A. z e. X
        ( ( ( x H y ) H z ) = ( x H ( y H z ) ) /\
          ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) /\
          ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) /\
        E. x e. X A. y e. X ( ( x H y ) = y /\ ( y H x ) = y ) ) ) ) $=
      ( crngo wcel cop wa cv co wceq wral cfv cvv cabl cxp wf wrex c1st relrngo
      w3a c2nd wrel 1st2nd mpan opeq12i syl6reqr id eqeltrd fvex eqeltri isrngo
      wb ax-mp sylib ) DKLZEFMZKLZEUALGGUBGFUCNAOZBOZFPZCOZFPVEVFVHFPZFPQVEVFVH
      EPFPVGVEVHFPZEPQVEVFEPVHFPVJVIEPQUGCGRBGRAGRVGVFQVFVEFPVFQNBGRAGUDNNZVBVC
      DKVBDDUESZDUHSZMZVCKUIVBDVNQUFDKUJUKEVLFVMHIULUMVBUNUOFTLVDVKUSFVMTIDUHUP
      UQABCTEFGJURUTVA $.
      $( [21-Dec-2013] $) $( [8-Sep-2007] $)

    $( Functionality of the multiplication operation of a ring.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    rngosm $p |- ( R e. RingOps -> H : ( X X. X ) --> X ) $=
      ( vx vy vz crngo wcel cabl cxp wf wa cv co wceq wral rngoi simpld simprd
      w3a wrex ) AKLZBMLZDDNDCOZUFUGUHPHQZIQZCRZJQZCRUIUJULCRZCRSUIUJULBRCRUKUI
      ULCRZBRSUIUJBRULCRUNUMBRSUDJDTIDTHDTUKUJSUJUICRUJSPIDTHDUEPHIJABCDEFGUAUB
      UC $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)

    $( Closure of the multiplication operation of a ring.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    rngocl $p |- ( ( R e. RingOps /\ A e. X /\ B e. X ) -> ( A H B ) e. X ) $=
      ( crngo wcel cxp wf co rngosm fovrn syl3an1 ) CJKFFLFEMAFKBFKABENFKCDEFGH
      IOABFFFEPQ $.
      $( [9-Sep-2007] $)

    $( The multiplication operation of a unital ring has (one or more) identity
       elements.  (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngoid $p |- ( ( R e. RingOps /\ A e. X ) ->
                 E. u e. X ( ( u H A ) = A /\ ( A H u ) = A ) ) $=
      ( vx vy wcel cv co wceq wa wrex wral simprd eqeq12d crngo w3a cabl cxp wf
      rngoi r19.12 syl oveq2 id oveq1 anbi12d rexbidv rcla4cva sylan ) CUALZAMZ
      JMZENZUROZURUQENZUROZPZAFQZJFRZBFLUQBENZBOZBUQENZBOZPZAFQZUPVCJFRAFQZVEUP
      USKMZENUQURVMENZENOUQURVMDNENUSUQVMENZDNOUQURDNVMENVOVNDNOUBKFRJFRAFRZVLU
      PDUCLFFUDFEUEPVPVLPAJKCDEFGHIUFSSVCAJFFUGUHVDVKJBFURBOZVCVJAFVQUTVGVBVIVQ
      USVFURBURBUQEUIVQUJZTVQVAVHURBURBUQEUKVRTULUMUNUO $.
      $( [22-Dec-2013] $) $( [8-Sep-2007] $)

    $( The unit element of a ring is unique. $)
    rngoideu $p |- ( R e. RingOps ->
                 E! u e. X A. x e. X ( ( u H x ) = x /\ ( x H u ) = x ) ) $=
      ( vy wcel cv co wceq wa wral ralimi oveq2 id eqeq12d crngo wrex wreu cabl
      wi cxp w3a rngoi simprr syl simpl rcla4v syl5 simpr oveq1 im2anan9r eqtr2
      wf eqcomd syl6 rgen2 jctir eqeq1d anbi12d ralbidv reu4 sylibr ) CUAKZBLZA
      LZEMZVJNZVJVIEMZVJNZOZAFPZBFUBZVPJLZVJEMZVJNZVJVREMZVJNZOZAFPZOZVIVRNZUEZ
      JFPBFPZOVPBFUCVHVQWHVHDUDKFFUFFEUROZVKVREMVIWAEMNVIVJVRDMEMVKVIVREMZDMNVI
      VJDMVREMWJWADMNUGJFPAFPBFPZVQOOVQBAJCDEFGHIUHWIWKVQUIUJWGBJFFVIFKZVRFKZOW
      EWJVRNZWJVINZOZWFWMVPWNWLWDWOVPVLAFPWMWNVOVLAFVLVNUKQVLWNAVRFVJVRNZVKWJVJ
      VRVJVRVIERWQSTULUMWDWBAFPWLWOWCWBAFVTWBUNQWBWOAVIFVJVINZWAWJVJVIVJVIVREUO
      WRSTULUMUPWPVRVIWJVRVIUQUSUTVAVBVPWDBJFWFVOWCAFWFVLVTVNWBWFVKVSVJVIVRVJEU
      OVCWFVMWAVJVIVRVJERVCVDVEVFVG $.
      $( [21-Dec-2013] $) $( [4-Apr-2009] $)

    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngodi $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                     ( A H ( B G C ) ) = ( ( A H B ) G ( A H C ) ) ) $=
      ( vx vy vz wcel co wceq cv wral wa oveq1 w3a crngo wrex cabl cxp wf rngoi
      simprd simpld simp2 ralimi oveq12d eqeq12d oveq2d oveq1d rcla43v impcom
      oveq2 syl5 ) AGNBGNCGNUAZDUBNZABCEOZFOZABFOZACFOZEOZPZVAKQZLQZFOZMQZFOVHV
      IVKFOZFOPZVHVIVKEOZFOZVJVHVKFOZEOZPZVHVIEOVKFOVPVLEOPZUAZMGRZLGRZKGRZUTVG
      VAWCVJVIPVIVHFOVIPSLGRKGUCZVAEUDNGGUEGFUFSWCWDSKLMDEFGHIJUGUHUIWCVRMGRZLG
      RZKGRUTVGWBWFKGWAWELGVTVRMGVMVRVSUJUKUKUKVRVGAVNFOZAVIFOZAVKFOZEOZPABVKEO
      ZFOZVDWIEOZPKLMABCGGGVHAPZVOWGVQWJVHAVNFTWNVJWHVPWIEVHAVIFTVHAVKFTULUMVIB
      PZWGWLWJWMWOVNWKAFVIBVKETUNWOWHVDWIEVIBAFURUOUMVKCPZWLVCWMVFWPWKVBAFVKCBE
      URUNWPWIVEVDEVKCAFURUNUMUPUSUSUQ $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)

    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngodir $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A G B ) H C ) = ( ( A H C ) G ( B H C ) ) ) $=
      ( vx vy vz wcel co wceq cv wral wa oveq2 w3a crngo wrex cabl cxp wf rngoi
      simprd simpld simp3 ralimi oveq1d eqeq12d oveq2d oveq12d rcla43v impcom
      oveq1 syl5 ) AGNBGNCGNUAZDUBNZABEOZCFOZACFOZBCFOZEOZPZVAKQZLQZFOZMQZFOVHV
      IVKFOZFOPZVHVIVKEOFOVJVHVKFOZEOPZVHVIEOZVKFOZVNVLEOZPZUAZMGRZLGRZKGRZUTVG
      VAWCVJVIPVIVHFOVIPSLGRKGUCZVAEUDNGGUEGFUFSWCWDSKLMDEFGHIJUGUHUIWCVSMGRZLG
      RZKGRUTVGWBWFKGWAWELGVTVSMGVMVOVSUJUKUKUKVSVGAVIEOZVKFOZAVKFOZVLEOZPVBVKF
      OZWIBVKFOZEOZPKLMABCGGGVHAPZVQWHVRWJWNVPWGVKFVHAVIEURULWNVNWIVLEVHAVKFURU
      LUMVIBPZWHWKWJWMWOWGVBVKFVIBAETULWOVLWLWIEVIBVKFURUNUMVKCPZWKVCWMVFVKCVBF
      TWPWIVDWLVEEVKCAFTVKCBFTUOUMUPUSUSUQ $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)

    $( Associative law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngoass $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A H B ) H C ) = ( A H ( B H C ) ) ) $=
      ( vx vy vz wcel co wceq cv wral wa ralimi w3a crngo wrex cxp rngoi simprd
      cabl wf simpld simp1 syl oveq1 oveq1d eqeq12d oveq2 oveq2d rcla43v impcom
      syl5 ) AGNBGNCGNUAZDUBNZABFOZCFOZABCFOZFOZPZVAKQZLQZFOZMQZFOZVGVHVJFOZFOZ
      PZMGRZLGRZKGRZUTVFVAVNVGVHVJEOFOVIVGVJFOZEOPZVGVHEOVJFOVRVLEOPZUAZMGRZLGR
      ZKGRZVQVAWDVIVHPVHVGFOVHPSLGRKGUCZVAEUGNGGUDGFUHSWDWESKLMDEFGHIJUEUFUIWCV
      PKGWBVOLGWAVNMGVNVSVTUJTTTUKVNVFAVHFOZVJFOZAVLFOZPVBVJFOZABVJFOZFOZPKLMAB
      CGGGVGAPZVKWGVMWHWLVIWFVJFVGAVHFULUMVGAVLFULUNVHBPZWGWIWHWKWMWFVBVJFVHBAF
      UOUMWMVLWJAFVHBVJFULUPUNVJCPZWIVCWKVEVJCVBFUOWNWJVDAFVJCBFUOUPUNUQUSUR $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)

    $( A ring element plus itself is two times the element.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    rngo2 $p |- ( ( R e. RingOps /\ A e. X ) ->
                  E. x e. X ( A G A ) = ( ( x G x ) H A ) ) $=
      ( crngo wcel wa cv co wceq wrex rngoid oveq12 anidms eqcomd simpll simplr
      simpr rngodir syl13anc eqeq2d syl5ibr adantrd reximdva mpd ) CJKZBFKZLZAM
      ZBENZBOZBUNENBOZLZAFPBBDNZUNUNDNBENZOZAFPABCDEFGHIQUMURVAAFUMUNFKZLZUPVAU
      QUPVAVCUSUOUODNZOUPVDUSUPVDUSOUOBUOBDRSTVCUTVDUSVCUKVBVBULUTVDOUKULVBUAUM
      VBUCZVEUKULVBUBUNUNBCDEFGHIUDUEUFUGUHUIUJ $.
      $( [22-Dec-2013] $) $( [9-Sep-2007] $)
  $}

  ${
    $d G x y z $.  $d R x y z $.
    ringabl.1 $e |- G = ( 1st ` R ) $.
    $( A ring's addition operation is an Abelian group operation.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    rngoablo $p |- ( R e. RingOps -> G e. AbelOp ) $=
      ( vx vy vz crngo wcel cabl crn cxp c2nd cfv wa cv wceq wral eqid simpld
      co wf w3a wrex rngoi ) AGHZBIHZBJZUGKUGALMZUAZUEUFUINDOZEOZUHTZFOZUHTUJUK
      UMUHTZUHTPUJUKUMBTUHTULUJUMUHTZBTPUJUKBTUMUHTUOUNBTPUBFUGQEUGQDUGQULUKPUK
      UJUHTUKPNEUGQDUGUCNDEFABUHUGCUHRUGRUDSS $.
      $( [21-Dec-2013] $) $( [9-Sep-2007] $)
  $}

  ${
    ringgrp.1 $e |- G = ( 1st ` R ) $.
    $( A ring's addition operation is a group operation.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    rngogrpo $p |- ( R e. RingOps -> G e. GrpOp ) $=
      ( crngo wcel cabl cgr rngoablo ablogrpo syl ) ADEBFEBGEABCHBIJ $.
      $( [9-Sep-2007] $)
  $}

  ${
    ringgcl.1 $e |- G = ( 1st ` R ) $.
    ringgcl.2 $e |- X = ran G $.
    $( Closure law for the addition (group) operation of a ring.  (Contributed
       by Steve Rodriguez, 9-Sep-2007.) $)
    rngogcl $p |- ( ( R e. RingOps /\ A e. X /\ B e. X ) -> ( A G B ) e. X ) $=
      ( crngo wcel cgr co rngogrpo grpocl syl3an1 ) CHIDJIAEIBEIABDKEICDFLABDEG
      MN $.
      $( [9-Sep-2007] $)

    $( The addition operation of a ring is commutative.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    rngocom $p |- ( ( R e. RingOps /\ A e. X /\ B e. X ) ->
                      ( A G B ) = ( B G A ) ) $=
      ( crngo wcel cabl co wceq rngoablo ablocom syl3an1 ) CHIDJIAEIBEIABDKBADK
      LCDFMABDEGNO $.
      $( [9-Sep-2007] $)

    $( The addition operation of a ring is associative.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    rngoaass $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                        ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( crngo wcel cgr w3a co wceq rngogrpo grpoass sylan ) DIJEKJAFJBFJCFJLABE
      MCEMABCEMEMNDEGOABCEFHPQ $.
      $( [9-Sep-2007] $)

    $( The addition operation of a ring is commutative.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    rngoa32 $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                      ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( crngo wcel cabl w3a co wceq rngoablo ablo32 sylan ) DIJEKJAFJBFJCFJLABE
      MCEMACEMBEMNDEGOABCEFHPQ $.
      $( [9-Sep-2007] $)

    $( Rearrangement of 4 terms in a sum of ring elements.  (Contributed by
       Steve Rodriguez, 9-Sep-2007.) $)
    rngoa4 $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X ) /\
                   ( C e. X /\ D e. X ) ) ->
                     ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( crngo wcel cabl wa co wceq rngoablo ablo4 syl3an1 ) EJKFLKAGKBGKMCGKDGK
      MABFNCDFNFNACFNBDFNFNOEFHPABCDFGIQR $.
      $( [9-Sep-2007] $)

    $( Right cancellation law for the addition operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngorcan $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                       ( ( A G C ) = ( B G C ) <-> A = B ) ) $=
      ( crngo wcel cgr w3a co wceq wb rngogrpo grporcan sylan ) DIJEKJAFJBFJCFJ
      LACEMBCEMNABNODEGPABCEFHQR $.
      $( [9-Sep-2007] $)

    $( Left cancellation law for the addition operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngolcan $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                       ( ( C G A ) = ( C G B ) <-> A = B ) ) $=
      ( crngo wcel cgr w3a co wceq wb rngogrpo grpolcan sylan ) DIJEKJAFJBFJCFJ
      LCAEMCBEMNABNODEGPABCEFHQR $.
      $( [9-Sep-2007] $)
  $}

  ${
    ring0cl.1 $e |- G = ( 1st ` R ) $.
    ring0cl.2 $e |- X = ran G $.
    ring0cl.3 $e |- Z = ( Id ` G ) $.
    $( A ring has an additive identity element.  (Contributed by Steve
       Rodriguez, 9-Sep-2007.) $)
    rngo0cl $p |- ( R e. RingOps -> Z e. X ) $=
      ( crngo wcel cgr rngogrpo grpoidcl syl ) AHIBJIDCIABEKDBCFGLM $.
      $( [9-Sep-2007] $)

    $( The additive identity of a ring is a right identity element.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngo0rid $p |- ( ( R e. RingOps /\ A e. X ) -> ( A G Z ) = A ) $=
      ( crngo wcel cgr co wceq rngogrpo grporid sylan ) BIJCKJADJAECLAMBCFNAECD
      GHOP $.
      $( [9-Sep-2007] $)

    $( The additive identity of a ring is a left identity element.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngo0lid $p |- ( ( R e. RingOps /\ A e. X ) -> ( Z G A ) = A ) $=
      ( crngo wcel cgr co wceq rngogrpo grpolid sylan ) BIJCKJADJEACLAMBCFNAECD
      GHOP $.
      $( [9-Sep-2007] $)
  $}

  ${
    ringlz.1 $e |- Z = ( Id ` G ) $.
    ringlz.2 $e |- X = ran G $.
    ringlz.3 $e |- G = ( 1st ` R ) $.
    ringlz.4 $e |- H = ( 2nd ` R ) $.
    $( The zero of a unital ring is a left absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    rngolz $p |- ( ( R e. RingOps /\ A e. X ) -> ( Z H A ) = Z ) $=
      ( crngo wcel wa co wceq cgr rngogrpo grpoidcl grpolid adantr mpdan oveq1d
      syl rngo0cl simpr 3jca rngodir syldan simpl rngocl syl3anc grporid eqcomd
      w3a syl2anc 3eqtr3d wb grpolcan syl13anc mpbid ) BKLZAELZMZFADNZVDCNZVDFC
      NZOZVDFOZVCFFCNZADNZVDVEVFVCVIFADVAVIFOZVBVACPLZVKBCIQZVLFELZVKFCEHGRFFCE
      HGSUAUCTUBVAVBVNVNVBUNVJVEOVCVNVNVBVAVNVBBCEFIHGUDTZVOVAVBUEZUFFFABCDEIJH
      UGUHVCVLVDELZVDVFOVAVLVBVMTZVCVAVNVBVQVAVBUIVOVPFABCDEIJHUJUKZVLVQMVFVDVD
      FCEHGULUMUOUPVCVLVQVNVQVGVHUQVRVSVOVSVDFVDCEHURUSUT $.
      $( [31-Aug-2009] $)

    $( The zero of a unital ring is a right absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    rngorz $p |- ( ( R e. RingOps /\ A e. X ) -> ( A H Z ) = Z ) $=
      ( crngo wcel wa co wceq cgr rngogrpo grpoidcl grpolid adantr mpdan oveq2d
      syl w3a simpr rngo0cl rngodi syldan rngocl mpd3an3 eqcomd syl2anc 3eqtr3d
      3jca wb grporcan syl13anc mpbid ) BKLZAELZMZAFDNZVBCNZFVBCNZOZVBFOZVAAFFC
      NZDNZVBVCVDVAVGFADUSVGFOZUTUSCPLZVIBCIQZVJFELZVIFCEHGRFFCEHGSUAUCTUBUSUTU
      TVLVLUDVHVCOVAUTVLVLUSUTUEUSVLUTBCEFIHGUFTZVMUNAFFBCDEIJHUGUHVAVJVBELZVBV
      DOUSVJUTVKTZUSUTVLVNVMAFBCDEIJHUIUJZVJVNMVDVBVBFCEHGSUKULUMVAVJVNVLVNVEVF
      UOVOVPVMVPVBFVBCEHUPUQUR $.
      $( [31-Aug-2009] $)
  $}

