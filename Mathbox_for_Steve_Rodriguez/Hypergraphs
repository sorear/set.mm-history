$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Hypergraphs
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c HypGrph $. $( class of hypergraphs $)

  $( Extend class notation with the class of all hypergraphs. $)
  chgra $a class HypGrph $.

  $(
  [Diestel] Diestel, Reinhard, "Graph Theory, Electronic Edition,"
  Springer-Verlag, Heidelberg, 2005 [QA166.D51413]
  URL: http://www.math.uni-hamburg.de/home/diestel/books/graph.theory/
  $)

  $(
  [McKMegPav] B. McKay, N. Megill, and M. Pavicic,
  "Algorithms for Greechie Diagrams," Int. J. Theor. Phys. 39,
  2393-2417 (2000), http://xxx.lanl.gov/abs/quant-ph/0009039 .
  $)

  ${
    $d a b $.
    $( Define the class of all hypergraphs.  For a textbook definition of a
       hypergraph and more information, see ~ ishgrag . $)
    df-hgra $a |- HypGrph = { <. a , b >. | ( ( a i^i b ) = (/)
           /\ b C_ ( ~P a \ { (/) } ) ) } $.
  $}

  ${
    $d b A $.  $d b B $.
    $( Two ways of saying "the blocks in a hypergraph are each non-empty
       subsets of the set of atoms in the hypergraph." $)
    blkssatm $p |- ( B C_ ( ~P A \ { (/) } ) <->
                    A. b e. B ( b C_ A /\ b =/= (/) ) ) $=
      ( cpw c0 csn cdif wss cv wcel wral wne wa dfss3 eldifsn visset elpw
      anbi1i bitri ralbii ) BADZEFGZHCIZUBJZCBKUCAHZUCELZMZCBKCBUBNUDUGCBUDUCUA
      JZUFMUGUCUAEOUHUEUFUCACPQRSTS $.
      $( [16-Dec-2006] $)
  $}

  ${
    $d x y $.
    $( The class of all hypergraphs is a relation. $)
    hgrarel $p |- Rel HypGrph $=
      ( vx vy chgra wrel cv cin c0 wceq cpw csn cdif wss wa copab relopab
      df-hgra releqi mpbir ) CDAEZBEZFGHTSIGJKLMZABNZDUAABOCUBABPQR $.
      $( [15-Dec-2006] $)
  $}

  ${
    $d x y A $.  $d x B $.  $d y B $.  $d b A $.  $d b B $.
    $( x and y are dummy. $)
    ishgrag.1 $e |- H = <. A , B >. $.
    $( Express the predicate " ` H ` is a hypergraph."  Definition of
       hypergraph in [Diestel] p. 28, which states "A hypergraph is a pair
       (_V_, _E_) of disjoint sets, where the elements of _E_ are non-empty
       subsets (of any cardinality) of _V_."

       Because _V_ and _E_ are both used as symbols (for the universal class
       ~ df-v and the epsilon relation ~ df-eprel , respectively) in Metamath,
       we instead use ` A ` to represent _V_, the set of _vertices_ or _atoms_
       of the hypergraph, and ` B ` to represent _E_, the set of _edges_ or
       _blocks_ that each connect one or more atoms in ` A ` .  (See Definition
       2.1 in [McKMegPav] p. 2384 for an analogous use of _atom_ and _block_ in
       Greechie diagrams.) $)
    ishgrag $p |- ( ( A e. C /\ B e. D ) ->
                   ( H e. HypGrph <->
                     ( ( A i^i B ) = (/) /\
                       A. b e. B ( b C_ A /\ b =/= (/) ) ) ) ) $=
      ( vx vy wcel wa chgra cin c0 wceq cpw csn cdif wss cv wne wral cop copab
      ineq1 eqeq1d pweq difeq1d sseq2d anbi12d ineq2 sseq1 opelopabg df-hgra
      eleq2i syl5bb eleq1i blkssatm anbi2i syl6bb ) ACJBDJKZELJZABMZNOZBAPZNQZR
      ZSZKZVDFTZASVJNUAKFBUBZKVAABUCZLJZVIVBVAVLHTZITZMZNOZVOVNPZVFRZSZKZHIUDZJ
      VIVMWAAVOMZNOZVOVGSZKVIHIABCDVNAOZVQWDVTWEWFVPWCNVNAVOUEUFWFVSVGVOWFVRVEV
      FVNAUGUHUIUJVOBOZWDVDWEVHWGWCVCNVOBAUKUFVOBVGULUJUMLWBVLHIUNUOUPEVLLGUQUP
      VHVKVDABFURUSUT $.
      $( [16-Dec-2006] $)
  $}

  ${
    $d x y A $.  $d x B $.  $d y B $.  $d x H $.  $d y H $.
    $( x and y are dummy. $)
    hgralem.1 $e |- A = ( 1st ` H ) $.
    hgralem.2 $e |- B = ( 2nd ` H ) $.
    $( Lemma for various hypergraph theorems. $)
    hgralem $p |- ( H e. HypGrph ->
                    ( ( A i^i B ) = (/) /\
                      B C_ ( ~P A \ { (/) } ) ) ) $=
      ( vx vy chgra wcel cv cin c0 wceq cpw csn cdif wss wa copab df-hgra
      eleq2i c1st cfv eqeq2i ineq1 sylbir eqeq1d wb pweq difeq1d sseq2d
      anbi12d c2nd ineq2 sseq1 elopabi sylbi ) CHICFJZGJZKZLMZUSURNZLOZPZQZRZFG
      SZIABKZLMZBANZVCPZQZRZHVGCFGTUAVFAUSKZLMZUSVKQZRVMFGCURCUBUCZMZVAVOVEVPVR
      UTVNLVRURAMZUTVNMAVQURDUDZURAUSUEUFUGVRVSVEVPUHVTVSVDVKUSVSVBVJVCURAUIUJU
      KUFULUSCUMUCZMZVOVIVPVLWBVNVHLWBUSBMZVNVHMBWAUSEUDZUSBAUNUFUGWBWCVPVLUHWD
      USBVKUOUFULUPUQ $.
      $( [15-Dec-2006] $)
  $}

  ${
    hgradj.1 $e |- A = ( 1st ` H ) $.
    hgradj.2 $e |- B = ( 2nd ` H ) $.
    $( The sets of atoms and blocks in a hypergraph are disjoint. $)
    hgradj $p |- ( H e. HypGrph -> ( A i^i B ) = (/) ) $=
      ( chgra wcel cin c0 wceq cpw csn cdif wss hgralem simpld ) CFGABHIJBAKILM
      NABCDEOP $.
      $( [17-Dec-2006] $)
  $}

  ${
    hgrablkconn.1 $e |- A = ( 1st ` H ) $.
    hgrablkconn.2 $e |- B = ( 2nd ` H ) $.
    $( The blocks in a hypergraph each connect to one or more atoms. $)
    hgrablkconn $p |- ( H e. HypGrph -> B C_ ( ~P A \ { (/) } ) ) $=
      ( chgra wcel cin c0 wceq cpw csn cdif wss hgralem simprd ) CFGABHIJBAKILM
      NABCDEOP $.
      $( [17-Dec-2006] $)
  $}

  ${
    $d H b $.  $d B b $.
    hgrablkne0.1 $e |- B = ( 2nd ` H ) $.
    $( The empty set cannot be a block in a hypergraph. $)
    hgrablkne0 $p |- ( H e. HypGrph -> A. b e. B b =/= (/) ) $=
      ( chgra wcel cv c0 wne c1st cfv wss wa wral wi cpw csn cdif eqid
      hgrablkconn blkssatm sylib ra4 simpr imim2i 3syl r19.21aiv ) BEFZCGZHIZCA
      UHUIBJKZLZUJMZCANZUIAFZUMOUOUJOUHAUKPHQRLUNUKABUKSDTUKACUAUBUMCAUCUMUJUOU
      LUJUDUEUFUG $.
      $( [17-Dec-2006] $)

    $( The number of atoms incident to each block of a hypergraph is greater
       than zero. $)
    hgrablkcard $p |- ( H e. HypGrph ->
                        A. b e. B (/) e. ( card ` b ) ) $=
      ( chgra wcel c0 cv ccrd cfv wne wi wral hgrablkne0 ra4 syl csdm wbr cvv
      wb 0ex cardsdom mpan card0 eleq1i syl5bbr 0sdomg bitrd pm5.74i sylibr
      r19.21aiv ) BEFZGCHZIJZFZCAULUMAFZUMGKZLZUPUOLULUQCAMURABCDNUQCAOPUPUOUQU
      PUOGUMQRZUQUPGIJZUNFZUSUOGSFUPVAUSTUAGUMSAUBUCUTGUNUDUEUFUMAUGUHUIUJUK $.
      $( [17-Dec-2006] $)
  $}

