$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Introduce the Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u t $.
    $( Axiom of Choice.  The Axiom of Choice (AC) is usually considered an
       extension of ZF set theory rather than a proper part of it.  It is
       sometimes considered philosophically controversial because it asserts
       the existence of a set without telling us what the set is.  ZF set
       theory that includes AC is called ZFC.

       The unpublished version given here says that given any set ` x ` , there
       exists a ` y ` that is a collection of unordered pairs, one pair for
       each non-empty member of ` x ` .  One entry in the pair is the member of
       ` x ` , and the other entry is some arbitrary member of that member of
       ` x ` .  See the rewritten version ~ ac3 for a more detailed
       explanation.  Theorem ~ ac2 shows an equivalent written compactly with
       restricted quantifiers.

       This version was specifically crafted to be short when expanded to
       primitives.  Kurt Maes' 5-quantifier version ~ ackm is slightly shorter
       when the biconditional of ~ ax-ac is expanded into implication and
       negation.

       Standard textbook versions of AC are derived as ~ ac8 , ~ ac5 , and
       ~ ac7 .  The Axiom of Regularity ~ ax-reg (among others) is used to
       derive our version from the standard ones; this reverse derivation is
       shown as theorem ~ aceq6b .  Equivalents to AC are the well-ordering
       theorem ~ weth and Zorn's lemma ~ zorn .  See ~ ac4 for comments about
       stronger versions of AC. $)
    ax-ac $a |- E. y A. z A. w ( ( z e. w /\ w e. x ) -> E. v A. u ( E. t
              ( ( u e. w /\ w e. t ) /\ ( u e. t /\ t e. y ) ) <-> u = v ) ) $.

    $( Axiom of Choice expressed with fewest number of different variables.
       The penultimate step shows the logical equivalence to ~ ax-ac . $)
    zfac $p |- E. x A. y A. z ( ( y e. z /\ z e. w ) -> E. w A. y ( E. w
              ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) $=
      ( vu vt vv cv wcel wa wex wceq wb wal elequ2 elequ1 anbi12d cbvexv syl6bb
      wi ax-ac equequ2 bibi2d anbi2d bibi1i albidv anbi1d exbidv equequ1 cbvalv
      bibi12d imbi2i 2albii exbii mpbi ) BHZCHZIZUQDHZIZJZEHZUQIZUQFHZIZJZVBVDI
      ZVDAHZIZJZJZFKZVBGHZLZMZENZGKZTZCNBNZAKVAVAUPUSIZUSVHIZJZJZDKZUPUSLZMZBNZ
      DKZTZCNBNZAKDABCGEFUAVSWJAVRWIBCVQWHVAVPWGGDVMUSLZVPVCUTJZVBUSIZWAJZJZDKZ
      VBUSLZMZENWGWKVOWREWKVOVLWQMWRWKVNWQVLGDEUBUCVLWPWQVKWOFDVDUSLZVFWLVJWNWS
      VEUTVCFDCOUDWSVGWMVIWAFDEOFDAPQQRUESUFWRWFEBVBUPLZWPWDWQWEWTWOWCDWTWLVAWN
      WBWTVCURUTEBCPUGWTWMVTWAEBDPUGQUHEBDUIUKUJSRULUMUNUO $.
      $( [14-Aug-2003] $)

    $( Axiom of Choice equivalent.  By using restricted quantifiers, we can
       express the Axiom of Choice with a single explicit conjunction.  (If you
       want to figure it out, the rewritten equivalent ~ ac3 is easier to
       understand.)  Note: ~ aceq0 shows the logical equivalence to
       ~ ax-ac . $)
    ac2 $p |- E. y A. z e. x A. w e. z E! v e. z E. u e. y
              ( z e. u /\ v e. u ) $=
      ( vt cv wcel wa wrex wreu wral wex wceq wb wal wi ax-ac aceq0 mpbir ) CHZ
      FHZIEHZUCIJFBHZKEUBLDUBMCAHZMBNUBDHZIUGUFIJUCUGIUGGHZIJUCUHIUHUEIJJGNUCUD
      OPFQENRDQCQBNABCDEFGSABCDEFGTUA $.
      $( [18-Jul-1996] $)
  $}

  ${
    $d x y z w v u $.
    $( Axiom of Choice using abbreviations.  The logical equivalence to ~ ax-ac
       can be established by chaining ~ aceq0 and ~ aceq2 .  A standard
       textbook version of AC is derived from this one in ~ aceq6a , and this
       version of AC is derived from the textbook version in ~ aceq6b .

       The following sketch will help you understand this version of the
       axiom.  Given any set ` x ` , the axiom says that there exists a ` y `
       that is a collection of unordered pairs, one pair for each non-empty
       member of ` x ` .  One entry in the pair is the member of ` x ` , and
       the other entry is some arbitrary member of that member of ` x ` .
       Using the Axiom of Regularity, we can show that ` y ` is really a set of
       _ordered_ pairs, very similar to the ordered pair construction
       ~ opthreg .  The key theorem for this (used in the proof of ~ aceq6b )
       is ~ preleq .  With this modified definition of ordered pair, it can be
       seen that ` y ` is actually a choice function on the members of ` x ` .

       For example, suppose
       ` x = { { 1 , 2 } , { 1 , 3 } , { 2 , 3 , 4 } } ` .  Let us try
       ` y = { { { 1 , 2 } , 1 } , { { 1 , 3 } , 1 } , `
       ` { { 2 , 3 , 4 } , 2 } } ` .  For the member (of ` x ` )
       ` z = { 1 , 2 } ` , the only assignment to ` w ` and ` v ` that
       satisfies the axiom is ` w = 1 ` and ` v = { { 1 , 2 } , 1 } ` , so
       there is exactly one ` w ` as required.  We verify the other two members
       of ` x ` similarly.  Thus ` y ` satisfies the axiom.  Using our modified
       ordered pair definition, we can say that ` y ` corresponds to the choice
       function ` { <. { 1 , 2 } , 1 >. , <. { 1 , 3 } , 1 >. , `
       ` <. { 2 , 3 , 4 } , 2 >. } ` .  Of course other choices for ` y ` will
       also satisfy the axiom, for example
       ` y = { { { 1 , 2 } , 2 } , { { 1 , 3 } , 1 } , `
       ` { { 2 , 3 , 4 } , 4 } } ` .  What AC tells us is that there exists at
       least one such ` y ` , but it doesn't tell us which one. $)
    ac3 $p |- E. y A. z e. x ( z =/= (/) -> E! w e. z E. v e. y
              ( z e. v /\ w e. v ) ) $=
      ( vu cv wcel wa wrex wreu wral wex c0 wne wi ac2 aceq2 mpbi ) CGZFGZHEGZU
      AHIFBGZJETKDTLCAGZLBMTNOTUBHDGUBHIEUCJDTKPCUDLBMABCDEFQABCDEFRS $.
      $( [9-Apr-2007] $) $( [19-Jul-1996] $)
  $}

  ${
    $d f x y z w v u $.
    $( An Axiom of Choice equivalent similar to the Axiom of Choice (first
       form) of [Enderton] p. 49. $)
    ac7 $p |- E. f ( f C_ x /\ f Fn dom x ) $=
      ( vz vu vv vy vw cv wcel wa wrex wreu wral wex wss cdm wfn wal c0 wne ac2
      wi aceq2 albii aceq6a sylbi 19.21bi mpg ) CHZDHZIEHZUJIJDFHZKEUILGUIMCAHZ
      MFNZBHZUMOUOUMPQJBNZAUNARZUPAUQUISTUIUKIGHUKIJEULKGUILUBCUMMFNZARUPARUNUR
      AAFCGEDUCUDAFCGEBUEUFUGAFCGEDUAUH $.
      $( [7-Mar-2007] $) $( [29-Apr-2004] $)
  $}

  ${
    $d f x R $.
    $( An Axiom of Choice equivalent similar to the Axiom of Choice (first
       form) of [Enderton] p. 49. $)
    ac7g $p |- ( R e. A -> E. f ( f C_ R /\ f Fn dom R ) ) $=
      ( vx cv wss cdm wfn wex wceq sseq2 dmeq fneq2d anbi12d exbidv ac7 vtoclg
      wa ) CEZDEZFZSTGZHZRZCISBFZSBGZHZRZCIDBATBJZUDUHCUIUAUEUCUGTBSKUIUBUFSTBL
      MNODCPQ $.
      $( [23-Jul-2004] $)
  $}

  ${
    $d x z f $.
    $( Equivalent of Axiom of Choice.  We do not insist that ` f ` be a
       function.  However, theorem ~ ac5 , derived from this one, shows that
       this form of the axiom does imply that at least one such set ` f ` whose
       existence we assert is in fact a function.  Axiom of Choice of
       [TakeutiZaring] p. 83.

       Takeuti and Zaring call this "weak choice" in contrast to "strong
       choice" ` E. F A. z ( z =/= (/) -> ( F `` z ) e. z ) ` , which asserts
       the existence of a universal choice function but requires second-order
       quantification on (proper) class variable ` F ` and thus cannot be
       expressed in our first-order formalization.  However, it has been shown
       that ZF plus strong choice is a conservative extension of ZF plus weak
       choice.  See Ulrich Felgner, "Comparison of the axioms of local and
       universal choice," _Fundamenta Mathematica_, 71, 43-62 (1971).

       Weak choice can be strengthened in a different direction to choose from
       a collection of proper classes; see ~ ac6s5 . $)
    ac4 $p |- E. f A. z e. x ( z =/= (/) -> ( f ` z ) e. z ) $=
      ( cv c0 wne cfv wcel wi wral wex wss cdm wfn wa wal aceq3 ac7 mpgbi a4i )
      BDZEFUACDZGUAHIBADZJCKZAUBUCLUBUCMNOCKUDAPAABCQACRST $.
      $( [30-Mar-2008] $) $( [21-Jul-1996] $)
  $}

  ${
    $d x y f A $.
    ac4c.1 $e |- A e. _V $.
    $( Equivalent of Axiom of Choice (class version) $)
    ac4c $p |- E. f A. x e. A ( x =/= (/) -> ( f ` x ) e. x ) $=
      ( vy cv c0 wne cfv wcel wi wral wex wceq raleq exbidv ac4 vtocl ) AFZGHSC
      FISJKZAEFZLZCMTABLZCMEBDUABNUBUCCTAUABOPEACQR $.
      $( [3-May-2007] $) $( [10-Feb-1997] $)
  $}

  ${
    $d f x y A $.
    ac5.1 $e |- A e. _V $.
    $( An Axiom of Choice equivalent: there exists a function ` f ` (called a
       choice function) with domain ` A ` that maps each nonempty member of the
       domain to an element of that member.  Axiom AC of [BellMachover]
       p. 488.  Note that the assertion that ` f ` be a function is not
       necessary; see ~ ac4 . $)
    ac5 $p |- E. f ( f Fn A /\ A. x e. A ( x =/= (/) -> ( f ` x ) e. x ) ) $=
      ( vy cv wfn c0 wne cfv wcel wi wral wa wex wceq fneq2 raleq anbi12d wal
      exbidv wss cdm aceq3 ac4 mpgbir aceq4 mpbi a4i vtocl ) CFZEFZGZAFZHIUNUKJ
      UNKLZAULMZNZCOZUKBGZUOABMZNZCOEBDULBPZUQVACVBUMUSUPUTULBUKQUOAULBRSUAUREU
      KULUBUKULUCGNCOETZURETVCUPCOEEACUDEACUEUFEACUGUHUIUJ $.
      $( [25-Apr-2007] $) $( [29-Aug-1999] $)
  $}

  ${
    $d x f A $.
    ac5b.1 $e |- A e. _V $.
    $( Equivalent of Axiom of Choice. $)
    ac5b $p |- ( A. x e. A x =/= (/) ->
               E. f ( f : A --> U. A /\ A. x e. A ( f ` x ) e. x ) ) $=
      ( cv c0 wne wral wfn cfv wcel wi wa wex cuni wf ac5 19.42v r19.26 sylbir
      pm3.35 ralimi crn wss chfnrn anc2li df-f syl6ibr impac sylan2 an12s eximi
      ex mpan2 ) AEZFGZABHZCEZBIZUPUOURJUOKZLZABHZMZCNZBBOZURPZUTABHZMZCNZABCDQ
      UQVDMUQVCMZCNVIUQVCCRVJVHCUSUQVBVHUQVBMZUSVGVHVKUPVAMZABHVGUPVAABSVLUTABU
      PUTUAUBTUSVGVFUSVGUSURUCVEUDZMVFUSVGVMUSVGVMABURUEUMUFBVEURUGUHUIUJUKULTU
      N $.
      $( [30-Apr-2007] $) $( [31-Aug-1999] $)
  $}

  ${
    $d f g x z w A $.  $d f g x y z w B $.  $d ph z w $.  $d y ps $.
    ac6.1 $e |- A e. _V $.
    ac6.2 $e |- B e. _V $.
    ${
      $d z f g C $.  $d z f g H $.
      ac6lem.4 $e |- C = { y e. B | ph } $.
      ac6lem.5 $e |- H = { <. x , z >. | ( x e. A /\ z = C ) } $.
      $( Lemma for ~ ac6 .  (Unnecessary distinct variable restrictions were
         removed by David Abernethy, 8-Apr-2014.) $)
      ac6lem $p |- ( A. x e. A E. y e. B ph ->
                   E. f ( f : A --> B /\ A. x e. A ( f ` x ) e. C ) ) $=
        ( vg wral wf cfv wcel wa wceq wrex cv wne crn cuni wex abid copab rneqi
        c0 cab rnopab eqtri eleq2i df-rex 3bitr4i wi eqeq2i biimpi neeq1d rabn0
        crab syl6bb biimprcd ralimi r19.23v sylib syl5bi ralrimiv fopabex2 rnex
        ac5b wss unissb ssrab2 eqsstri sseq1 mpbiri rexlimivw sylbi mprgbir fss
        ccom mpan2 wfn cvv rabex eqeltri fnopab2 dffn3 mpbi fco wfun ffun ax-17
        adantr hbopab1 hbxfreq hbrn hbral hban 19.8a abeq2i sylibr expcom eleq1
        sylibd vtocle fveq2 id eleq12d rcla4v syl impcom adantll wb cdm dmopab2
        fnfun ax-mp mp3an2 sylan2br fveq1i fvopab2 syl5eq fveq2d adantl adantlr
        fvco eqtrd eleq1d mpbird ex ralrimi sylan jca vex coex feq1 hbco hbeleq
        fveq1 ralbid anbi12d cla4ev exlimiv 3syl ) ACFUAZBEOZDUBZUJUCZDIUDZOUUL
        UULUEZNUBZPZUUJUUNQZUUJRZDUULOZSZNUFEFHUBZPZBUBZUUTQZGRZBEOZSZHUFZUUIUU
        KDUULUUJUULRZUUJGTZBEUAZUUIUUKUUJUVBERZUVISZBUFZDUKZRUVMUVHUVJUVMDUGUUL
        UVNUUJUULUVLBDUHZUDUVNIUVOMUIUVLBDULUMZUNUVIBEUOUPZUUIUVIUUKUQZBEOUVJUU
        KUQUUHUVRBEUVIUUKUUHUVIUUKACFVBZUJUCUUHUVIUUJUVSUJUVIUUJUVSTGUVSUUJLURU
        SUTACFVAVCVDVEUVIUUKBEVFVGVHVIDUULNIBDEGIJMVJZVKVLUUSUVGNUUSEFUUNIWCZPZ
        UVBUWAQZGRZBEOZSZUVGUUOUULFUUNPZUURUWFUUOUUMFVMZUWGUWHUUJFVMZDUULDUULFV
        NUVHUVJUWIUVQUVIUWIBEUVIUWIGFVMGUVSFLACFVOVPUUJGFVQVRVSVTWAUULUUMFUUNWB
        WDUWGUURSUWBUWEUWGUWBUURUWGEUULIPZUWBIEWEZUWJBDEGIGUVSWFLACFKWGWHZMWIZE
        IWJWKEUULFUUNIWLWDWPUWGUUNWMZUURUWEUULFUUNWNUWNUURSZUWDBEUWNUURBUWNBWOU
        UQBDUULBDIBDIUVOMUVLBDDWQWRWSUUQBWOWTXAUWOUVKUWDUWOUVKSUWDGUUNQZGRZUURU
        VKUWQUWNUVKUURUWQUVKGUULRZUURUWQUQUVKUWRUQDGUWLUVIUVKUVHUWRUVKUVIUVHUVL
        UVMUVHUVLBXBUVMDUULUVPXCXDXEUUJGUULXFXGXHUUQUWQDGUULUVIUUPUWPUUJGUUJGUU
        NXIUVIXJXKXLXMXNXOUWNUVKUWDUWQXPUURUWNUVKSZUWCUWPGUWSUWCUVBIQZUUNQZUWPU
        VKUWNUVBIXQZRZUWCUXATZUXBEUVBBDEGIUWLMXRUNUWNIWMZUXCUXDUWKUXEUWMEIXSXTU
        VBUUNIYIYAYBUVKUXAUWPTUWNUVKUWTGUUNUVKUWTUVBUVOQZGUVBIUVOMYCUVKGWFRUXFG
        TUWLBDEGWFYDWDYEYFYGYJYKYHYLYMYNYOYPYOUVFUWFHUWAUUNINYQUVTYRUUTUWATZUVA
        UWBUVEUWEEFUUTUWAYSUXGUVDUWDBEBHUWABHUUNIUUTUUNRBWOBHIUVOMUVLBDHWQWRYTU
        UAUXGUVCUWCGUVBUUTUWAUUBYKUUCUUDUUEXMUUFUUG $.
        $( [8-Apr-2014] $) $( [18-Oct-1999] $)
    $}

    ${
      $d f ph $.
      ac6.3 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
      $( Equivalent of Axiom of Choice.  This is useful for proving that there
         exists, for example, a sequence mapping natural numbers to members of
         a larger set ` B ` , where ` ph ` depends on ` x ` (the natural
         number) and ` y ` (to specify a member of ` B ` ).  A stronger version
         of this theorem, ~ ac6s , allows ` B ` to be a proper class. $)
      ac6 $p |- ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
        ( vz vw wrex wral cv wf wcel wa wex wceq cfv crab copab eqeq1 cbvopab2v
        eqid anbi2d ac6lem elrab simprbi ralimi anim2i eximi syl ) ADFMCENEFGOZ
        PZCOZUOUAZADFUBZQZCENZRZGSUPBCENZRZGSACDKEFUSGUQEQZLOZUSTZRZCLUCHIUSUFV
        HVEKOZUSTZRCLKVFVITVGVJVEVFVIUSUDUGUEUHVBVDGVAVCUPUTBCEUTURFQBABDURFJUI
        UJUKULUMUN $.
        $( [18-Oct-1999] $)
    $}
  $}

  ${
    $d A f x y z $.  $d B f y z $.
    ac6c4.1 $e |- A e. _V $.
    ac6c4.2 $e |- B e. _V $.
    $( Equivalent of Axiom of Choice. ` B ` is a collection ` B ( x ) ` of
       nonempty sets.  (Contributed by Mario Carneiro, 22-Mar-2013.) $)
    ac6c4 $p |- ( A. x e. A B =/= (/) ->
               E. f ( f Fn A /\ A. x e. A ( f ` x ) e. B ) ) $=
      ( vy vz c0 wne wral cv csb wcel wrex cfv wa wex ax-17 cbvral ciun wfn vex
      wf wel hbcsb1 hbne weq csbeq1a neeq1d n0 hbre1 eleq2d rcla4e eliun sylibr
      ra4e sylancom ex exlimd syl5bi ralimia sylbi iunex eleq1 ac6 hbel eleq12d
      ffn fveq2 biimpri anim12i eximi 3syl ) CIJZABKZGLZAHLZCMZNZGABCUAZOZHBKZB
      WADLZUDZVRWDPZVSNZHBKZQZDRWDBUBZALZWDPZCNZABKZQZDRVPVSIJZHBKWCVOWPAHBVOHS
      AGVSIAGVRCHUCGHUEASUFZVQINASUGAHUHZCVSIAVRCUIZUJTWPWBHBWPVTGRVRBNZWBGVSUK
      WTVTWBGWTGSVTGWAULWTVTWBWTVTVQWANZWBWTVTQVQCNZABOXAXBVTAVRBWQWRCVSVQWSUMU
      NAVQBCUOUPVTGWAUQURUSUTVAVBVCVTWGHGBWADEABCEFVDVQWFVSVEVFWIWODWEWJWHWNBWA
      WDVIWNWHWMWGAHBWMHSAGGWFVSVQWFNASWQVGWRWLWFCVSWKVRWDVJWSVHTVKVLVMVN $.
      $( [22-Mar-2013] $)

    $( Equivalent of Axiom of Choice. ` B ` is a collection ` B ( x ) ` of
       nonempty sets.  Remark after Theorem 10.46 of [TakeutiZaring] p. 98.
       (Contributed by Mario Carneiro, 22-Mar-2013.) $)
    ac6c5 $p |- ( A. x e. A B =/= (/) -> E. f A. x e. A ( f ` x ) e. B ) $=
      ( c0 wne wral cv wfn cfv wcel wa wex ac6c4 simpr eximi syl ) CGHABIDJZBKZ
      AJTLCMABIZNZDOUBDOABCDEFPUCUBDUAUBQRS $.
      $( [22-Mar-2013] $)

    $d A f x $.  $d B f $.
    $( An Axiom of Choice equivalent: the infinite Cartesian product of
       nonempty classes is nonempty.  Axiom of Choice (second form) of
       [Enderton] p. 55 and its converse.  (Contributed by Mario Carneiro,
       22-Mar-2013.) $)
    ac9 $p |- ( A. x e. A B =/= (/) <-> X_ x e. A B =/= (/) ) $=
      ( vf c0 wne wral cixp cv wfn cfv wcel wa wex ac6c4 wceq wn df-ne n0 elixp
      vex exbii bitr2i sylib wrex ixp0 con3i ralbii ralnex bitri 3imtr4i impbii
      ) CGHZABIZABCJZGHZUPFKZBLAKUSMCNABIOZFPZURABCFDEQURUSUQNZFPVAFUQUAVBUTFAB
      CUSFUCUBUDUEUFUQGRZSCGRZABUGZSZURUPVEVCABCUHUIUQGTUPVDSZABIVFUOVGABCGTUJV
      DABUKULUMUN $.
      $( [22-Mar-2013] $)
  $}

  ${
    $d x f z A $.  $d x y f z B $.  $d f z ph $.  $d y z ps $.
    ac6s.1 $e |- A e. _V $.
    ac6s.2 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( Equivalent of Axiom of Choice.  Using the Boundedness Axiom ~ bnd2 , we
       derive this strong version of ~ ac6 that doesn't require ` B ` to be a
       set. $)
    ac6s $p |- ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
      ( vz wrex wral cv wss wa wex wf bnd2 vex ac6 anim2i fss expcom anim1d imp
      eximi eximdv exlimiv 3syl ) ADFKCELJMZFNZADUJKCELZOZJPUKEUJGMZQZBCELZOZGP
      ZOZJPEFUNQZUPOZGPZACDJEFHRUMUSJULURUKABCDEUJGHJSITUAUFUSVBJUKURVBUKUQVAGU
      KUOUTUPUOUKUTEUJFUNUBUCUDUGUEUHUI $.
      $( [4-Feb-2004] $)

    $( Equivalent of Axiom of Choice.  Contrapositive of ~ ac6s . $)
    ac6n $p |- ( A. f ( f : A --> B -> E. x e. A ps ) ->
                E. x e. A A. y e. B ph ) $=
      ( cv wf wn wral wa wex wrex wi wal cfv bitri wceq notbid ac6s con3i albii
      dfrex2 imbi2i alinexa dfral2 rexbii rexnal 3imtr4i ) EFGJZKZBLZCEMZNGOZLZ
      ALZDFPZCEMZLZUNBCEPZQZGRZADFMZCEPZVAUQUSUOCDEFGHDJCJUMSUAABIUBUCUDVEUNUPL
      ZQZGRURVDVIGVCVHUNBCEUFUGUEUNUPGUHTVGUTLZCEPVBVFVJCEADFUIUJUTCEUKTUL $.
      $( [10-Jun-2007] $)

    $( Generalization of the Axiom of Choice to classes.  Slightly strengthened
       version of ~ ac6s3 . $)
    ac6s2 $p |- ( A. x e. A E. y ph -> E. f ( f Fn A /\ A. x e. A ps ) ) $=
      ( wex wral cvv wrex cv wfn wa rexv ralbii wf ac6s ffn anim1i eximi sylbir
      syl ) ADIZCEJADKLZCEJZFMZENZBCEJZOZFIZUFUECEADPQUGEKUHRZUJOZFIULABCDEKFGH
      SUNUKFUMUIUJEKUHTUAUBUDUC $.
      $( [29-Sep-2006] $)

    $( Generalization of the Axiom of Choice to classes.  Theorem 10.46 of
       [TakeutiZaring] p. 97. $)
    ac6s3 $p |- ( A. x e. A E. y ph -> E. f A. x e. A ps ) $=
      ( wex wral cv wfn wa ac6s2 simpr eximi syl ) ADICEJFKELZBCEJZMZFISFIABCDE
      FGHNTSFRSOPQ $.
      $( [3-Nov-2004] $)
  $}

  ${
    $d A f x z $.  $d B f x y z $.  $d f z ph $.  $d y z ps $.
    ac6sg.1 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( ~ ac6s with sethood as antecedent.  (Contributed by FL, 3-Aug-2009.) $)
    ac6sg $p |- ( A e. V -> ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) ) $=
      ( vz wrex cv wral wf wa wex wi wceq raleq feq2 anbi12d exbidv imbi12d vex
      ac6s vtoclg ) ADFKZCJLZMZUHFGLZNZBCUHMZOZGPZQUGCEMZEFUJNZBCEMZOZGPZQJEHUH
      ERZUIUOUNUSUGCUHESUTUMURGUTUKUPULUQUHEFUJTBCUHESUAUBUCABCDUHFGJUDIUEUF $.
      $( [2-Aug-2009] $)
  $}

  ${
    $d f x z A $.  $d x y f z B $.  $d f z ph $.  $d z ps $.
    ac6sf.1 $e |- ( ps -> A. y ps ) $.
    ac6sf.2 $e |- A e. _V $.
    ac6sf.3 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( Version of ~ ac6 with bound-variable hypothesis. $)
    ac6sf $p |- ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
      ( vz wrex wral cv wsbc wf wa wex cbvrexsv ralbii cfv sbhypf ac6s sylbi )
      ADFLZCEMADKNOZKFLZCEMEFGNZPBCEMQGRUEUGCEADKFSTUFBCKEFGIABDKCNUHUAHJUBUCUD
      $.
      $( [2-Mar-2008] $)
  $}

  ${
    $d f x y A $.  $d f y B $.
    ac6s4.1 $e |- A e. _V $.
    $( Generalization of the Axiom of Choice to proper classes. ` B ` is a
       collection ` B ( x ) ` of nonempty, possible proper classes. $)
    ac6s4 $p |- ( A. x e. A B =/= (/) ->
               E. f ( f Fn A /\ A. x e. A ( f ` x ) e. B ) ) $=
      ( vy c0 wne wral cv wcel wex wfn cfv wa n0 ralbii eleq1 ac6s2 sylbi ) CGH
      ZABIFJZCKZFLZABIDJZBMAJUENZCKZABIODLUAUDABFCPQUCUGAFBDEUBUFCRST $.
      $( [29-Sep-2006] $)

    $( Generalization of the Axiom of Choice to proper classes. ` B ` is a
       collection ` B ( x ) ` of nonempty, possible proper classes.  Remark
       after Theorem 10.46 of [TakeutiZaring] p. 98. $)
    ac6s5 $p |- ( A. x e. A B =/= (/) -> E. f A. x e. A ( f ` x ) e. B ) $=
      ( c0 wne wral cv wfn cfv wcel wa wex ac6s4 simpr eximi syl ) CFGABHDIZBJZ
      AISKCLABHZMZDNUADNABCDEOUBUADTUAPQR $.
      $( [19-Nov-2006] $) $( [27-Mar-2006] $)
  $}

  ${
    $d x z y w v f $.
    $( An Axiom of Choice equivalent.  Given a family ` x ` of mutually
       disjoint nonempty sets, there exists a set ` y ` containing exactly one
       member from each set in the family.  Theorem 6M(4) of [Enderton]
       p. 151. $)
    ac8 $p |- ( ( A. z e. x z =/= (/) /\
                   A. z e. x A. w e. x ( z =/= w -> ( z i^i w ) = (/) ) ) ->
               E. y A. z e. x E! v v e. ( z i^i y ) ) $=
      ( vf cv c0 wne wral cin wceq wi wa wcel weu wex wss cdm wfn wal aceq5 ac7
      mpgbi a4i ) CGZHICAGZJUFDGZIUFUHKHLMDUGJCUGJNEGUFBGKOEPCUGJBQMZAFGZUGRUJU
      GSTNFQUIAUAAABCDEFUBAFUCUDUE $.
      $( [22-Apr-2007] $) $( [14-May-2004] $)
  $}

  ${
    $d f x A $.  $d f B $.
    ac9.1 $e |- A e. _V $.
    $( An Axiom of Choice equivalent: the infinite Cartesian product of
       nonempty classes is nonempty.  Axiom of Choice (second form) of
       [Enderton] p. 55 and its converse.  This is a stronger version of the
       axiom in Enderton, with no existence requirement for the family of
       classes ` B ( x ) ` (achieved via the Collection Principle ~ cp ). $)
    ac9s $p |- ( A. x e. A B =/= (/) <-> X_ x e. A B =/= (/) ) $=
      ( vf c0 wne wral cixp cv wfn cfv wcel wa wex ac6s4 n0 wceq wn df-ne elixp
      vex exbii bitr2i sylib wrex ixp0 con3i ralbii ralnex bitri 3imtr4i impbii
      ) CFGZABHZABCIZFGZUOEJZBKAJURLCMABHNZEOZUQABCEDPUQURUPMZEOUTEUPQVAUSEABCU
      REUBUAUCUDUEUPFRZSCFRZABUFZSZUQUOVDVBABCUGUHUPFTUOVCSZABHVEUNVFABCFTUIVCA
      BUJUKULUM $.
      $( [29-Sep-2006] $)
  $}

  ${
    $d v A $.  $d u v x y ph $.  $d ps x v $.  $d u v w x y z $.
    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, 1 => 2. $)
    kmlem1 $p |- ( A. x ( ( A. z e. x z =/= (/) /\ A. z e. x A. w e. x ph ) ->
                 E. y A. z e. x ps ) -> A. x ( A. z e. x A. w e. x ph ->
                   E. y A. z e. x ( z =/= (/) -> ps ) ) ) $=
      ( vv vu cv c0 wne wral wa wex wi wal wceq raleq wcel ralimi2 crab anbi12d
      rabex raleqbi1dv exbidv imbi12d cla4v alrimiv ssrab2 sseli imim1i imim12i
      vex neeq1 elrab simprbi rgen jctil biimpri exp3a eximi alimi cbvalv sylib
      syl ) EIZJKZECIZLZAFVHLZEVHLZMZBEVHLZDNZOZCPZAFGIZLZEVQLZVGBOZEVQLZDNZOZG
      PZVKVTEVHLZDNZOZCPVPVGEHIZJKZHVQUAZLZAFWJLZEWJLZMZBEWJLZDNZOZGPWDVPWQGVOW
      QCWJWIHVQGUMUCVHWJQZVLWNVNWPWRVIWKVKWMVGEVHWJRVJWLEVHWJAFVHWJRUDUBWRVMWOD
      BEVHWJRUEUFUGUHWQWCGVSWNWPWBVSWMWKVRWLEVQWJVFWJSZVFVQSZVRWLWJVQVFWIHVQUIZ
      UJAAFVQWJFIZWJSXBVQSAWJVQXBXAUJUKTULTVGEWJWSWTVGWIVGHVFVQWHVFJUNUOZUPUQUR
      WOWADBVTEWJVQWSBOWTVGBWTVGMZWSBWSXDXCUSUKUTTVAULVBVEWCWGGCVQVHQZVSVKWBWFV
      RVJEVQVHAFVQVHRUDXEWAWEDVTEVQVHRUEUFVCVD $.
      $( [12-May-2007] $) $( [5-Apr-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem2 $p |- ( E. y A. z e. x ( ph -> E! w w e. ( z i^i y ) ) <->
        E. y ( -. y e. x /\ A. z e. x ( ph -> E! w w e. ( z i^i y ) ) ) ) $=
      ( vv vu cv cin wcel weu wi wral wex wn wa wceq eleq2d eubidv imbi2d ineq2
      ralbidv cbvexv vex uniex eleq2 notbid exbidv wal nalset alexn mpbir vtocl
      cuni a4i csn cun indi c0 elssuni sseld con3d disjsn syl6ibr impcom uneq2d
      un0 syl6eq syl5req ralbidva snid olci elun mpi con3i biantrurd bitrd snex
      unex eleq1 anbi12d cla4ev syl6bi exlimiv ax-mp sylbi simpr eximi impbii
      wo ) AEHZDHZCHZIZJZEKZLZDBHZMZCNZWMWRJZOZWSPZCNZWTAWKWLFHZIZJZEKZLZDWRMZF
      NXDWSXJCFWMXEQZWQXIDWRXKWPXHAXKWOXGEXKWNXFWKWMXEWLUARSTUBUCXJXDFGHZWRUNZJ
      ZOZGNZXJXDLZXLWMJZOZGNZXPCXMWRBUDUEWMXMQZXSXOGYAXRXNWMXMXLUFUGUHXTCXTCUIX
      RGUICNOCGUJXRCGUKULUOUMXOXQGXOXJXEXLUPZUQZWRJZOZAWKWLYCIZJZEKZLZDWRMZPZXD
      XOXJYJYKXOXIYIDWRXOWLWRJZPZXHYHAYMXGYGEYMXFYFWKYMYFXFWLYBIZUQZXFWLXEYBURY
      MYOXFUSUQXFYMYNUSXFYLXOYNUSQZYLXOXLWLJZOYPYLYQXNYLWLXMXLWLWRUTVAVBWLXLVCV
      DVEVFXFVGVHVIRSTVJXOYEYJYDXNYDXLYCJZXNYRXLXEJZXLYBJZWJYTYSXLGUDVKVLXLXEYB
      VMULYDYCXMXLYCWRUTVAVNVOVPVQXCYKCYCXEYBFUDXLVRVSWMYCQZXBYEWSYJUUAXAYDWMYC
      WRVTUGUUAWQYIDWRUUAWPYHAUUAWOYGEUUAWNYFWKWMYCWLUARSTUBWAWBWCWDWEWDWFXCWSC
      XBWSWGWHWI $.
      $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4.  The
       right-hand side is part of the hypothesis of 4. $)
    kmlem3 $p |- ( ( z \ U. ( x \ { z } ) ) =/= (/) <->
        E. v e. z A. w e. x ( z =/= w -> -. v e. ( z i^i w ) ) ) $=
      ( cv cdif c0 wne wcel wn crab cun wa wex anbi1i anbi2i bitri ancom 3bitri
      wrex csn cuni cin wi wral wo dfdif2 dfnul3 uneq2i un0 unrab 3eqtr3i ianor
      wb eluni df-rex eldifsn necom anass an32 elin df-an bitr3i 3bitr3ri exbii
      19.41v rexnal 3bitr2ri con1bii a1i rabbiia 3eqtri neeq1i rabn0 ) BEZAEZVO
      UAFZUBZFZGHVOCEZHZDEZVOVTUCIZJUDZCVPUEZDVOKZGHWEDVOTVSWFGVSWBVRIZJZDVOKZW
      HWBVOIZJZUFZDVOKZWFDVOVRUGWIGLWIWKDVOKZLWIWMGWNWIDVOUHUIWIUJWHWKDVOUKULWL
      WEDVOWLWEUNWJWLWGWJMZJWEWGWJUMWEWOWOWBVTIZVTVQIZMZCNZWJMZWDJZCVPTZWEJWGWS
      WJCWBVQUOOXBVTVPIZXAMZCNWRWJMZCNWTXACVPUPXDXECWPWJMZWQMZXCWAWJWPMZMZMZXEX
      DXGXFXCWAMZMZXKXHMZXJWQXKXFWQXCVTVOHZMXKVTVPVOUQXNWAXCVTVOURPQPXLXHXKMXMX
      FXHXKWPWJROXHXKRQXCWAXHUSSWPWJWQUTXIXAXCXIWAWCMXAWCXHWAWBVOVTVAPWAWCVBVCP
      VDVEWRWJCVFSWDCVPVGVHVIVCVJVKVLVMWEDVOVNQ $.
      $( [12-Oct-2007] $) $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem4 $p |- ( ( w e. x /\ z =/= w ) ->
                    ( ( z \ U. ( x \ { z } ) ) i^i w ) = (/) ) $=
      ( vy vv cv wcel wne wa wn csn cdif cuni wral cin c0 wceq wi wal eldif wex
      simpr eluni notbii alnex con2b imnan eldifsn necom anbi2i 3bitr3i 3bitr2i
      bitri imbi1i albii sylib sylbi eleq1 neeq2 anbi12d eleq2 notbid a4v com12
      imbi12d syl5 ralrimiv disj sylibr ) CFZAFZGZBFZVJHZIZDFZVJGZJZDVMVKVMKLZM
      ZLZNWAVJOPQVOVRDWAVPWAGZEFZVKGZVMWCHZIZVPWCGZJZRZESZVOVRWBVPVMGZVPVTGZJZI
      ZWJVPVMVTTWNWMWJWKWMUBWMWGWCVSGZIZEUAZJWPJZESWJWLWQEVPVSUCUDWPEUEWRWIEWGW
      OJRWOWHRWRWIWGWOUFWGWOUGWOWFWHWOWDWCVMHZIWFWCVKVMUHWSWEWDWCVMUIUJUMUNUKUO
      ULUPUQWJVOVRWIVOVRRECWCVJQZWFVOWHVRWTWDVLWEVNWCVJVKURWCVJVMUSUTWTWGVQWCVJ
      VPVAVBVEVCVDVFVGDWAVJVHVI $.
      $( [12-Oct-2007] $) $( [26-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem5 $p |- ( ( w e. x /\ z =/= w ) ->
          ( ( z \ U. ( x \ { z } ) ) i^i ( w \ U. ( x \ { w } ) ) ) = (/) ) $=
      ( cv wcel wne wa csn cdif cuni cin c0 wss wceq difss sslin ax-mp syl5sseq
      kmlem4 ss0b sylib ) CDZADZEBDZUBFGZUDUCUDHIJIZUBUCUBHIJZIZKZLMUILNUEUFUBK
      ZUILUHUBMUIUJMUBUGOUHUBUFPQABCSRUITUA $.
      $( [12-Oct-2007] $) $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 4 => 1. $)
    kmlem6 $p |- ( ( A. z e. x z =/= (/) /\
           A. z e. x A. w e. x ( ph -> A = (/) ) ) ->
           A. z e. x E. v e. z A. w e. x ( ph -> -. v e. A ) ) $=
      ( cv c0 wne wral wceq wi wa wcel wn wrex r19.26 wex wal ralimi n0 alrimiv
      biimpi ne0i necon2bi imim2i 19.29r df-rex sylibr syl2an sylbir ) CGZHIZCB
      GZJAFHKZLZDUNJZCUNJMUMUQMZCUNJAEGZFNZOZLZDUNJZEULPZCUNJUMUQCUNQURVDCUNUMU
      SULNZERZVCESZVDUQUMVFEULUAUCUQVCEUPVBDUNUOVAAUTFHFUSUDUEUFTUBVFVGMVEVCMER
      VDVEVCEUGVCEULUHUIUJTUK $.
      $( [12-May-2007] $) $( [26-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 4 => 1. $)
    kmlem7 $p |- ( ( A. z e. x z =/= (/) /\
           A. z e. x A. w e. x ( z =/= w -> ( z i^i w ) = (/) ) ) ->
       -. E. z e. x A. v e. z E. w e. x ( z =/= w /\ v e. ( z i^i w ) ) ) $=
      ( cv c0 wne wral cin wceq wi wa wcel wn wrex kmlem6 ralinexa rexbii bitri
      rexnal ralbii ralnex sylib ) BEZFGBAEZHUDCEZGZUDUFIZFJKCUEHBUEHLUGDEUHMZN
      KCUEHZDUDOZBUEHZUGUILCUEOZDUDHZBUEONZUGABCDUHPULUNNZBUEHUOUKUPBUEUKUMNZDU
      DOUPUJUQDUDUGUICUEQRUMDUDTSUAUNBUEUBSUC $.
      $( [12-Oct-2007] $) $( [26-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4 1 <=> 4. $)
    kmlem8 $p |- ( ( -. E. z e. u A. w e. z ps -> E. y A. z e. u ( z =/= (/)
                  -> E! w w e. ( z i^i y ) ) ) <-> ( E. z e. u A. w e. z ps \/
                 E. y ( -. y e. u /\ A. z e. u E! w w e. ( z i^i y ) ) ) ) $=
      ( cv wral wrex wn c0 wne cin wcel wi wex wa wb sylbir ralimi syl wo biimt
      weu ralnex df-rex rexnal bitr3i exsimpl sylibr ralbi anbi2d exbidv kmlem2
      n0 syl6rbbr pm5.74i pm4.64 bitri ) ADCFZGZCEFZHZIZUSJKZDFZUSBFZLMDUCZNZCV
      AGZBOZNVCVFVAMIZVGCVAGZPZBOZNVBVNUAVCVJVNVCVDCVAGZVJVNQVCUTIZCVAGVOUTCVAU
      DVPVDCVAVPVEUSMZAIZPDOZVDVSVRDUSHVPVRDUSUEADUSUFUGVSVQDOVDVQVRDUHDUSUNUIR
      SRVOVNVKVIPZBOVJVOVMVTBVOVLVIVKVOVGVHQZCVAGVLVIQVDWACVAVDVGUBSVGVHCVAUJTU
      KULVDEBCDUMUOTUPVBVNUQUR $.
      $( [12-May-2007] $) $( [4-Apr-2004] $)
  $}

  ${
    $d x y z w v u t h g $.  $d y z w v h g A $.  $d h ph $.
    kmlem9.1 $e |- A = { u | E. t e. x u = ( t \ U. ( x \ { t } ) ) } $.
    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem9 $p |- A. z e. A A. w e. A ( z =/= w -> ( z i^i w ) = (/) ) $=
      ( vh cv wne cin c0 wceq wi wcel csn cdif cuni wrex wa eqeq1 rexbidv elab2
      vex difeq1 sneq difeq2d unieqd eqtrd eqeq2d cbvrexv reeanv eqeq12 syl5ibr
      bitri necon3d kmlem5 ineq12 eqeq1d exp3a syl5d com12 adantl sylbir rgen2a
      rexlimivv syl2anb ) BIZCIZJZVHVIKZLMZNZBCFVHFOVHEIZAIZVNPZQZRZQZMZEVOSZVI
      HIZVOWBPZQZRZQZMZHVOSZVMVIFOZDIZVSMZEVOSZWADVHFBUDWJVHMWKVTEVOWJVHVSUAUBG
      UCWIVIVSMZEVOSZWHWLWNDVIFCUDWJVIMWKWMEVOWJVIVSUAUBGUCWMWGEHVOVNWBMZVSWFVI
      WOVSWBVRQWFVNWBVRUEWOVRWEWBWOVQWDWOVPWCVOVNWBUFUGUHUGUIZUJUKUOWAWHTVTWGTZ
      HVOSEVOSVMVTWGEHVOVOULWQVMEHVOVOWBVOOZWQVMNVNVOOWQWRVMWQVJVNWBJZWRVLWQVNW
      BVHVIWOVHVIMWQVSWFMWPVHVSVIWFUMUNUPWQWRWSVLWRWSTVLWQVSWFKZLMAEHUQWQVKWTLV
      HVSVIWFURUSUNUTVAVBVCVFVDVGVE $.
      $( [12-Oct-2007] $) $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem10 $p |- ( A. h ( A. z e. h A. w e. h
                              ( z =/= w -> ( z i^i w ) = (/) ) ->
                         E. y A. z e. h ph ) -> E. y A. z e. A ph ) $=
      ( cv wne cin c0 wceq wi wral wex cdif raleq wal csn cuni wrex cab cvv vex
      kmlem9 abrexex eqeltri raleqbi1dv exbidv imbi12d cla4v mpi ) DKZEKZLUPUQM
      NOPZEIKZQZDUSQZADUSQZCRZPZIUAUREHQZDHQZADHQZCRZBDEFGHJUHVDVFVHPIHHFKGKZBK
      ZVIUBSUCSZOGVJUDFUEUFJGFVJVKBUGUIUJUSHOZVAVFVCVHUTVEDUSHUREUSHTUKVLVBVGCA
      DUSHTULUMUNUO $.
      $( [12-Oct-2007] $) $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem11 $p |- ( z e. x -> ( z i^i U. A ) = ( z \ U. ( x \ { z } ) ) ) $=
      ( cv wcel cuni cin csn cdif ciun wceq cvv vex eqtr4i c0 cun syl5eq unieqi
      wrex cab difexg ax-mp dfiun2 ineq2i iunin2 undif2 wss snssi ssequn1 sylib
      syl5req iuneq1 syl iunxun difeq1 sneq difeq2d unieqd ineq2d iunxsn uneq1i
      eqtrd eqtri wral wne eldifsn simprbi wa incom kmlem4 syl5 ralrimiv iuneq2
      ex iun0 syl6eq uneq2d un0 indif ) BGZAGZHZWCEIZJZDWDWCDGZWDWHKZLZIZLZJZMZ
      WCWDWCKZLZIZLZWGWCDWDWLMZJWNWFWSWCWFCGWLNDWDUBCUCZIWSEWTFUADCWDWLWHOHWLOH
      DPWHWKOUDUEUFQUGDWDWCWLUHQWEWNWCWRJZRSZWRWEWNDWOWPSZWMMZXBWEWDXCNWNXDNWEX
      CWOWDSZWDWOWDUIWEWOWDUJXEWDNWCWDUKWOWDULUMUNDWDXCWMUOUPWEXDXADWPWMMZSZXBX
      DDWOWMMZXFSXGDWOWPWMUQXHXAXFDWCWMXABPWHWCNZWLWRWCXIWLWCWKLWRWHWCWKURXIWKW
      QWCXIWJWPXIWIWOWDWHWCUSUTVAUTVEVBVCVDVFWEXFRXAWEXFDWPRMZRWEWMRNZDWPVGXFXJ
      NWEXKDWPWHWPHZWHWCVHZWEXKXLWHWDHXMWHWDWCVIVJWEXMXKWEXMVKWMWLWCJRWCWLVLADB
      VMTVQVNVODWPWMRVPUPDWPVRVSVTTVEXBXAWRXAWAWCWQWBVFVST $.
      $( [26-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem12 $p |- ( A. z e. x ( z \ U. ( x \ { z } ) ) =/= (/) ->
                    ( A. z e. A ( z =/= (/) -> E! v v e. ( z i^i y ) ) ->
        A. z e. x ( z =/= (/) -> E! v v e. ( z i^i ( y i^i U. A ) ) ) ) ) $=
      ( cv c0 wne cin wcel weu wi wral cdif cuni wceq wal csn cab raleqi df-ral
      wrex vex eqeq1 rexbidv elab imbi1i r19.23v bitr4i albii ralcom4 cvv ax-mp
      difexg neeq1 ineq1 eleq2d eubidv imbi12d ralbii 3bitri ralim sylbi difeq1
      ceqsalv 3bitr2i sneq difeq2d unieqd eqtrd neeq1d cbvralv in12 incom eqtri
      ineq1d imbi12i kmlem11 syl5req ax-1 syl6bi ralimia imim2i com12 syl5 ) CI
      ZJKZDIZWIBIZLZMZDNZOZCGPZFIZAIZWRUAZQZRZQZJKZFWSPZWKXCWLLZMZDNZFWSPZOZWIW
      SWIUAZQZRZQZJKZCWSPZWJWKWIWLGRZLLZMZDNZOZCWSPZWQXDXHOZFWSPZXJWQWPCEIZXCSZ
      FWSUEZEUBZPWIYHMZWPOZCTZYDWPCGYHHUCWPCYHUDYKWIXCSZWPOZFWSPZCTYMCTZFWSPYDY
      JYNCYJYLFWSUEZWPOYNYIYPWPYGYPEWICUFYEWISYFYLFWSYEWIXCUGUHUIUJYLWPFWSUKULU
      MYMFCWSUNYOYCFWSWPYCCXCWRUOMXCUOMFUFWRXBUOUQUPYLWJXDWOXHWIXCJURYLWNXGDYLW
      MXFWKWIXCWLUSUTVAVBVHVCVIVDXDXHFWSVEVFXJXPYBXJXPWKXNWLLZMZDNZCWSPZOXPYBOX
      EXPXIYTXDXOFCWSWRWISZXCXNJUUAXCWIXBQXNWRWIXBVGUUAXBXMWIUUAXAXLUUAWTXKWSWR
      WIVJVKVLVKVMZVNVOXHYSFCWSUUAXGYRDUUAXFYQWKUUAXCXNWLUUBVSUTVAVOVTYTYBXPYSY
      ACWSWIWSMZYSXTYAUUCYRXSDUUCYQXRWKUUCXRWIXQLZWLLZYQXRWLUUDLUUEWIWLXQVPWLUU
      DVQVRUUCUUDXNWLACEFGHWAVSWBUTVAXTWJWCWDWEWFVFWGWH $.
      $( [12-May-2007] $) $( [27-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4 1 <=> 4. $)
    kmlem13 $p |- ( A. x ( ( A. z e. x z =/= (/) /\
           A. z e. x A. w e. x ( z =/= w -> ( z i^i w ) = (/) ) ) ->
         E. y A. z e. x E! v v e. ( z i^i y ) ) <->
          A. x ( -. E. z e. x A. v e. z E. w e. x
                     ( z =/= w /\ v e. ( z i^i w ) ) ->
               E. y A. z e. x ( z =/= (/) ->
                    E! v v e. ( z i^i y ) ) ) ) $=
      ( vh vg cv wral cin wceq wi wcel wex wal wrex c0 wne wa weu wn raleqbi1dv
      kmlem1 raleq exbidv imbi12d cbvalv kmlem10 ineq2 eleq2d eubidv imbi2d csn
      cbvexv cdif cuni kmlem3 ralinexa rexbii rexnal 3bitri ralbii ralnex bitri
      ralbidv kmlem12 vex inex1 cla4ev syl6 exlimdv com12 syl5bir sylbi alrimiv
      syl kmlem7 imim1i biimt ralimi ralbi adantr pm5.74i sylibr alimi impbii
      wb ) CLZUAUBZCALZMZWLDLZUBZWLWPNZUAOPZDWNMZCWNMZUCZELZWLBLZNZQZEUDZCWNMZB
      RZPZASZWQXCWRQZUCDWNTZEWLMZCWNTUEZWMXGPZCWNMZBRZPZASZXKXAXRPZASZXTWSXGABC
      DUGYBWSDJLZMZCYCMZXPCYCMZBRZPZJSZXTYAYHAJWNYCOZXAYEXRYGWTYDCWNYCWSDWNYCUH
      UFYJXQYFBXPCWNYCUHUIUJUKYIXSAYIXPCHMZBRZXSXPABCDFGHJIULYLWMXCWLKLZNZQZEUD
      ZPZCHMZKRZXSYKYRBKXDYMOZXPYQCHYTXGYPWMYTXFYOEYTXEYNXCXDYMWLUMUNUOUPVIURXO
      WLWNWLUQUSUTUSUAUBZCWNMZYSXRUUBXNUEZCWNMXOUUAUUCCWNUUAWQXLUEPDWNMZEWLTXMU
      EZEWLTUUCACDEVAUUDUUEEWLWQXLDWNVBVCXMEWLVDVEVFXNCWNVGVHUUBYSXRUUBYRXRKUUB
      YRWMXCWLYMHUTZNZNZQZEUDZPZCWNMZXRAKCEFGHIVJXQUULBUUGYMUUFKVKVLXDUUGOZXPUU
      KCWNUUMXGUUJWMUUMXFUUIEUUMXEUUHXCXDUUGWLUMUNUOUPVIVMVNVOVPVQVRVTVSVRVTXSX
      JAXSXBXRPXJXBXOXRACDEWAWBXBXIXRWOXIXRWKXAWOXHXQBWOXGXPWKZCWNMXHXQWKWMUUNC
      WNWMXGWCWDXGXPCWNWEVTUIWFWGWHWIWJ $.
      $( [12-Oct-2007] $) $( [5-Apr-2004] $)
  $}

  ${
    $d x y z w v u $.  $d u ph $.
    kmlem14.1 $e |- ( ph <->
                ( z e. y -> ( ( v e. x /\ y =/= v ) /\ z e. v ) ) ) $.
    kmlem14.2 $e |- ( ps <-> ( z e. x -> ( ( v e. z /\ v e. y ) /\
                ( ( u e. z /\ u e. y ) -> u = v ) ) ) ) $.
    kmlem14.3 $e |- ( ch <->
                A. z e. x E! v v e. ( z i^i y ) ) $.
    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 5 <=> 4. $)
    kmlem14 $p |- ( E. z e. x A. v e. z E. w e. x
                   ( z =/= w /\ v e. ( z i^i w ) ) <->
                 E. y A. z E. v A. u ( y e. x /\ ph ) ) $=
      ( cv wne wcel wa wrex wex wal wi cin wral wceq neeq1 ineq1 eleq2d anbi12d
      rexbidv raleqbi1dv cbvrexv df-rex eleq1 anbi2d df-ral bitri anbi2i 19.28v
      cbvralv neeq2 ineq2 imbi2i 19.37v bitr4i 19.42v ax-17 baibr anass pm5.74i
      19.3 elin syl6bb bitr2i exbii 3bitr2i albii 3bitri ) FMZGMZNZHMZVQVRUAZOZ
      PZGDMZQZHVQUBZFWDQEMZVRNZVTWGVRUAZOZPZGWDQZHWGUBZEWDQWGWDOZWMPZERWNAPZISZ
      HRZFSZERWFWMFEWDWEWLHVQWGVQWGUCZWCWKGWDWTVSWHWBWJVQWGVRUDWTWAWIVTVQWGVRUE
      UFUGUHUIUJWMEWDUKWOWSEWOWNVQWGOZWHVQWIOZPZGWDQZTZFSZPWNXEPZFSWSWMXFWNWMXD
      FWGUBXFWLXDHFWGVTVQUCZWKXCGWDXHWJXBWHVTVQWIULUMUHURXDFWGUNUOUPWNXEFUQXGWR
      FXGWNXAVTWDOZWGVTNZVQWGVTUAZOZPZPZTZHRZPWNXOPZHRWRXEXPWNXEXAXNHRZTXPXDXRX
      AXDXMHWDQXRXCXMGHWDVRVTUCZWHXJXBXLVRVTWGUSXSWIXKVQVRVTWGUTUFUGUJXMHWDUKUO
      VAXAXNHVBVCUPWNXOHVDXQWQHWQWPXQWPIWPIVEVIAXOWNAXAXIXJPZVQVTOZPZTXOJXAYBXN
      XAYBXTXLPXNXAYAXLXTXLXAYAVQWGVTVJVFUMXIXJXLVGVKVHUOUPVLVMVNVOVNVMVP $.
      $( [12-Oct-2007] $) $( [4-Apr-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 5 <=> 4. $)
    kmlem15 $p |- ( ( -. y e. x /\ ch ) <->
                     A. z E. v A. u ( -. y e. x /\ ps ) ) $=
      ( cv wcel wa wal wex wi bitri albii 19.28v wn cin weu wral wceq ax-17 eu1
      wsbc elin clelsb3 eqcom imbi12i anbi12i bitr4i exbii ralbii df-ral 19.21v
      19.37v 3bitri anbi2i 19.42v bitr2i 3bitr2i ) ELZDLZMUAZCNVGBHOZGPZFOZNVGV
      INZFOVGBNHOZGPZFOCVJVGCGLZFLZVEUBZMZGUCZFVFUDVNVOMVNVEMNZHLZVOMVTVEMNZVTV
      NUEZQZNZHOZGPZFVFUDZVJKVRWFFVFVRVQVQGVTUHZVNVTUEZQZHOZNZGPWFVQGHVQHUFUGWL
      WEGWLVSWCHOZNWEVQVSWKWMVNVOVEUIWJWCHWHWAWIWBWHVTVPMWAHGVPUJVTVOVEUIRVNVTU
      KULSUMVSWCHTUNUORUPWGVOVFMZWFQZFOVJWFFVFUQVIWOFVIWNWEQZGPWOVHWPGVHWNWDQZH
      OWPBWQHJSWNWDHURRUOWNWEGUSRSUNUTVAVGVIFTVKVMFVMVGVHNZGPVKVLWRGVGBHTUOVGVH
      GVBVCSVD $.
      $( [4-Apr-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4 5 <=> 4. $)
    kmlem16 $p |- ( ( E. z e. x A. v e. z E. w e. x
       ( z =/= w /\ v e. ( z i^i w ) ) \/ E. y ( -. y e. x /\ ch ) ) <->
       E. y A. z E. v A. u ( ( y e. x /\ ph ) \/ ( -. y e. x /\ ps ) ) ) $=
      ( cv wcel wa wn wex wo wal exbii wne wrex wral kmlem14 kmlem15 orbi12i wb
      cin 19.43 pm3.24 simpl a4s exlimivv anim12i mto 19.33b ax-mp bitr2i albii
      exlimiv bitr3i 3bitr2i ) FMZGMZUAHMVCVDUHNOGDMZUBHVCUCFVEUBZEMVENZPZCOZEQ
      ZRVGAOZISZHQZFSZEQZVHBOZISZHQZFSZEQZRVNVSRZEQVKVPRISZHQZFSZEQVFVOVJVTABCD
      EFGHIJKLUDVIVSEABCDEFHIJKLUETUFVNVSEUIWAWDEWAVMVRRZFSZWDVMFQZVRFQZOZPWFWA
      UGWIVGVHOZVGUJZWGVGWHVHVLVGFHVKVGIVGAUKZULUMVQVHFHVPVHIVHBUKZULUMUNUOVMVR
      FUPUQWEWCFWCVLVQRZHQWEWBWNHVKIQZVPIQZOZPWBWNUGWQWJWKWOVGWPVHVKVGIWLUTVPVH
      IWMUTUNUOVKVPIUPUQTVLVQHUIURUSVATVB $.
      $( [12-Oct-2007] $) $( [4-Apr-2004] $)
  $}

  ${
    $d x y z w v u f t h $.
    $( Equivalence of the Axiom of Choice (first form) of [Enderton] p. 49 and
       Maes' AC ~ ackm .  The proof consists of lemmas ~ kmlem1 through
       ~ kmlem16 and this final theorem.  AC is not used for the proof.  Note:
       bypassing the first step (i.e. replacing ~ aceq5 with ~ biid )
       establishes the AC equivalence shown by Mae's writeup.  The
       left-hand-side AC shown here was chosen because it is shorter to
       display. $)
    aceqkm $p |- ( A. x E. f ( f C_ x /\ f Fn dom x ) <->
      A. x E. y A. z E. v A. u (
       ( y e. x /\ ( z e. y -> ( ( v e. x /\ -. y = v ) /\ z e. v ) ) ) \/
           ( -. y e. x /\ ( z e. x -> ( ( v e. z /\ v e. y ) /\
               ( ( u e. z /\ u e. y ) ->
                   u = v ) ) ) ) ) ) $=
      ( vw vt vh cv wa wex wal wne wral wceq wi wcel wrex wn wss cdm wfn c0 cin
      weu aceq5 csn cdif cuni cab eqid kmlem13 kmlem8 albii bitri bicomi anbi2i
      wo df-ne anbi1i imbi2i biid kmlem16 3bitri ) FJZAJZUAVFVGUBUCKFLAMCJZUDNZ
      CVGOVHGJZNZVHVJUEZUDPQGVGOCVGOKDJZVHBJZUERDUFZCVGOZBLQAMZVKVMVLRKGVGSZDVH
      OCVGSZVNVGRZTZVPKBLUSZAMZVTVHVNRZVMVGRZVNVMPTZKZVHVMRZKZQZKWAVHVGRVMVHRVM
      VNRKEJZVHRWKVNRKWKVMPQKQZKUSEMDLCMBLZAMABCGDFUGVQVSTVIVOQCVGOBLQZAMWCABCG
      DHIHJIJZVGWOUHUIUJUIPIVGSHUKZWPULUMWNWBAVRBCDAUNUOUPWBWMAWJWLVPABCGDEWIWE
      VNVMNZKZWHKWDWGWRWHWFWQWEWQWFVNVMUTUQURVAVBWLVCVPVCVDUOVE $.
      $( [13-Apr-2004] $)
  $}

  ${
    $d x y z w v u f t h $.
    $( A remarkable equivalent to the Axiom of Choice that has only 5
       quantifiers (when expanded to ` e. ` , ` = ` primitives in prenex form),
       discovered and proved by Kurt Maes.  This establishes a new record,
       reducing from 6 to 5 the largest number of quantified variables needed
       by any ZFC axiom.  The ZF-equivalence to AC is shown by theorem
       ~ aceqkm .  Maes found this version of AC in April, 2004 (replacing a
       longer version, also with 5 quantifiers, that he found in November,
       2003).  See Kurt Maes, "A 5-quantifier ( ` e. ` ,=)-expression
       ZF-equivalent to the Axiom of Choice"
       ( ~ http://arxiv.org/PS_cache/arxiv/pdf/0705/0705.3162v1.pdf ).

       The original FOM posts are:
       ~ http://www.cs.nyu.edu/pipermail/fom/2003-November/007631.html
       ~ http://www.cs.nyu.edu/pipermail/fom/2003-November/007641.html . $)
    ackm $p |- A. x E. y A. z E. v A. u (
       ( y e. x /\ ( z e. y -> ( ( v e. x /\ -. y = v ) /\ z e. v ) ) ) \/
           ( -. y e. x /\ ( z e. x -> ( ( v e. z /\ v e. y ) /\
               ( ( u e. z /\ u e. y ) ->
                   u = v ) ) ) ) ) $=
      ( vf cv wss cdm wfn wa wex wcel wceq wn wi wo wal aceqkm ac7 mpgbi ) FGZA
      GZHUBUCIJKFLBGZUCMZCGZUDMDGZUCMUDUGNOKUFUGMKPKUEOUFUCMUGUFMUGUDMKEGZUFMUH
      UDMKUHUGNPKPKQERDLCRBLARAABCDEFSAFTUA $.
      $( [29-Apr-2004] $)

$(
    @( DELETE THIS @)
    ackmxx @p |- ( A. x E. y A. z E. v A. u (
       ( y e. x /\ ( z e. y -> ( ( v e. x /\ -. y = v ) /\ z e. v ) ) ) \/
           ( -. y e. x /\ ( z e. x -> ( ( v e. z /\ v e. y ) /\
               ( ( u e. z /\ u e. y ) ->
                   u = v ) ) ) ) ) <-> ph ) @=
      ( cv wcel wceq wn wa wi wo wal wex df-ex df-or df-an imbi1i notbii bitri
      imbi2i imbi12i albii bicomi ) ACGZBGZHZDGZUFHZEGZUGHZUFUKIJZKZUIUKHZKZLZK
      ZUHJZUIUGHZUKUIHZUKUFHZKZFGZUIHZVDUFHZKZVDUKIZLZKZLZKZMZFNZEOZDNZCOZBNZAU
      HUJULUMJLJZUOJZLZJZLZJZLZJZJZUSUTVAVBJLJZVEVFJLJZVHLZJZLZJZLZJZLZJZLZFNZJ
      ZENZJZDNZJZCNZJZBNZVR?VRXGVQXFBVQVPJZCNZJXFVPCPXIXEXHXDCVPXCVOXBDVOVNJZEN
      ZJXBVNEPXKXAXJWTEVNWSVMWRFVMURJZVLLWRURVLQXLWGVLWQURWFURUHUQJZLZJWFUHUQRX
      NWEXMWDUHUQWCUPWBUJUPUNVTLZJWBUNUORXOWAUNVSVTULUMRSTUAUBTUBTUATVLUSVKJZLZ
      JWQUSVKRXQWPXPWOUSVKWNVJWMUTVJVCVIJZLZJWMVCVIRXSWLVCWHXRWKVAVBRVIWJVGWIVH
      VEVFRSTUCTUAUBTUBTUAUCUAUDTUDTUAUDTUDTUAUDUEUAUE @.
      @( [ ?] @) @( [2-Feb-2006] @)

    axacxx @p  |- ( E. y A. z A. w ( ( z e. w /\ w e. x ) -> E. v A. u ( E. t
              ( ( u e. w /\ w e. t ) /\ ( u e. t /\ t e. y ) ) <-> u = v ) )
<-> ph ) @=
      ( cv wcel wa wex wceq wb wal wi wn df-ex df-an dfbi1 notbii imbi12i bitri
      albii imbi1i imbi2i bicomi ) ADIEIZJZUHBIJZKZGIZUHJZUHHIZJZKZULUNJZUNCIJZ
      KZKZHLZULFIMZNZGOZFLZPZEOZDOZCLZAUIUJQPQZUMUOQPQZUQURQPQZQZPZQZQZHOZQZVBP
      ZVBVRPZQZPZQZGOZQZFOZQZPZEOZDOZQZCOZQZVI?VIWMVIVHQZCOZQWMVHCRWOWLWNWKCVHW
      JVGWIDVFWHEUKVJVEWGUIUJSVEVDQZFOZQWGVDFRWQWFWPWEFVDWDVCWCGVCVAVBPZVBVAPZQ
      ZPZQWCVAVBTXAWBWRVSWTWAVAVRVBVAUTQZHOZQVRUTHRXCVQXBVPHUTVOUTUPUSQZPZQVOUP
      USSXEVNUPVKXDVMUMUOSUSVLUQURSUAUBUAUCUAUDUAUCZUEWSVTVAVRVBXFUFUAUBUAUCUDU
      AUDUAUCUBUDUDUAUDUAUCUGUCUG @.
      @( [ ?] @) @( [2-Feb-2006] @)

Maes expanded to primitives -  94 symbols
A.  4
e. 10
-> 11
-. 17
(  11
)  11
=   2
var occ 28
vars 5

-. A. y -. A. z -. A. v -. A. u ( ( y e. x -> -. ( z e. y -> -. ( -. ( v
e. x -> y = v ) -> -. z e. v ) ) ) -> -. ( -. y e. x -> -. ( z e. x ->
-. ( -. ( v e. z -> -. v e. y ) -> -. ( -. ( u e. z -> -. u e. y ) -> u
= v ) ) ) ) )

ax-ac expanded to primitives - 99 symbols

A.  7
e. 10
-> 11
-. 16
(  11
)  11
=   2
var occ 31
vars 7

-. A. y -. A. z A. w ( -. ( z e. w -> -. w e. x ) -> -. A. v -. A. u -.
( ( -. A. t ( -. ( u e. w -> -. w e. t ) -> ( u e. t -> -. t e. y ) ) ->
u = v ) -> -. ( u = v -> -. A. t ( -. ( u e. w -> -. w e. t ) -> ( u e.
t -> -. t e. y ) ) ) ) )
$)
  $}

