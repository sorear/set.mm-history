$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
       AC equivalents:  well ordering, Zorn's lemma
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)
  ${
    $d x y z f g A $.  $d x y f B $.  $d x y f F $.  $d x y f G $.
    numthlem.1 $e |- A e. _V $.
    $( Hypotheses for transfinite recursion. $)
    numthlem.2 $e |- B = { f | E. x e. On ( f Fn x /\
                A. y e. x ( f ` y ) = ( G ` ( f |` y ) ) ) } $.
    numthlem.3 $e |- F = U. B $.
    $( Generator function.  ` g ` is a choice function on ` ~P A ` . $)
    numthlem.4 $e |- G = { <. f , y >. | y = ( g ` ( A \ ran f ) ) } $.
    $( Lemma for ~ numth . $)
    numthlem $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( cv c0 cfv wcel wi con0 wceq cvv wral wf1o wrex pwex ac4c cima cdif cres
      wne cpw wex wss difss ssexi elpw mpbir neeq1 fveq2 eleq12d imbi12d rcla4v
      wa id ax-mp imp tfr2 crn copab wfun tfrlem7 vex resfunexg mp2an fvex rneq
      difeq2 3syl fvopab fveq1i df-ima difeq2i fveq2i syl6eq eleq1d exp3a com12
      3eqtr4i syl5ibr ralrimiv tfr1 tz7.49c mpan f1oeq1 cla4ev reximi exlimiv )
      BMZNUIZWQFMZOZWQPZQZBCUJZUAZFUKAMZCEMZUBZEUKZARUCZBXCFCIUDUEXDXIFXDCGXEUF
      ZUGZNUIZXEGOZXKPZQZARUAZXECGXEUHZUBZARUCZXIXDXOARXERPZXDXOXTXDXLXNXDXLVBX
      NXTXKWSOZXKPZXDXLYBXKXCPZXDXLYBQZQYCXKCULCXJUMZXKCXKCIYEUNUOUPXBYDBXKXCWQ
      XKSZWRXLXAYBWQXKNUQYFWTYAWQXKWQXKWSURYFVCUSUTVAVDVEXTXMYAXKXTXMXQHOZYAABA
      DEGHJKVFXQWQCXFVGZUGZWSOZSEBVHZOCXQVGZUGZWSOZYGYAEBXQYJYNGVIXETPXQTPABDEG
      HJKVJAVKGXETVLVMZYMWSVNXFXQSYHYLSYIYMSYJYNSXFXQVOYHYLCVPYIYMWSURVQVRXQHYK
      LVSXKYMWSXJYLCGXEVTWAWBWGWCWDWHWEWFWICTPXPXSIACTGABDEGHJKWJWKWLXRXHARXGXR
      EXQYOXECXFXQWMWNWOVQWPVD $.
      $( [10-Feb-1997] $)
  $}

  ${
    $d x y z w v u f g h A $.
    numth.1 $e |- A e. _V $.
    $( Numeration theorem: every set can be put into one-to-one correspondence
       with some ordinal (using AC).  Theorem 10.3 of [TakeutiZaring] p. 84. $)
    numth $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( vy vg vz vw vu vv vh cv wfn cfv cres crn cdif wceq copab wral con0 wrex
      wa cab cuni rdglem1 eqid id rneq difeq2 3syl eqeqan12rd cbvopabv numthlem
      fveq2 ) AEBFLZGLZMHLZUPNUPUROILZBJLZPZQZKLZNZRZJISZNRHUQTUCGUAUBFUDZCKVGU
      EZVFDGHAEFCVFUFVHUGVEELZBCLZPZQZVCNZRJICEUSVIRZUTVJRZUSVIVDVMVNUHVOVAVKRV
      BVLRVDVMRUTVJUIVAVKBUJVBVLVCUOUKULUMUN $.
      $( [10-Feb-1997] $)
  $}

  ${
    $d x y z A $.
    $( Numeration theorem: any set is equinumerous to some ordinal (using AC).
       Theorem 10.3 of [TakeutiZaring] p. 84. $)
    numth2 $p |- E. x e. On x ~~ A $=
      ( vy vz cvv wcel cv cen wbr con0 wrex wceq breq2 rexbidv wex vex numth c0
      wf1o 0elon rexbii mpbir vtoclg wn enrefg ax-mp brprc mpbiri breq1 rcla4ev
      bren sylancr pm2.61i ) BEFZAGZBHIZAJKZUOCGZHIZAJKZUQCBEURBLUSUPAJURBUOHMN
      UTUOURDGSDOZAJKAURDCPZQUSVAAJUOURDVBUKUAUBUCUNUDZRJFZRBHIZUQTVCVERRHIZVDV
      FTRJUEUFRBHUGUHUPVEARJUORBHUIUJULUM $.
      $( [20-Oct-2003] $)
  $}

  ${
    $d x y A $.
    $( Any set is strictly dominated by some ordinal. $)
    numthcor $p |- ( A e. V -> E. x e. On A ~< x ) $=
      ( vy csdm wbr con0 wrex wceq breq1 rexbidv cpw cen numth2 vex canth2 pwex
      cv cvv ax-mp ensym wcel wa wi sdomentr sylancr reximi vtoclg ) DRZARZEFZA
      GHZBUJEFZAGHDBCUIBIUKUMAGUIBUJEJKUJUILZMFZAGHULAUNNUOUKAGUOUIUNEFZUNUJMFZ
      UKUIDOZPUJUNUIURQUAUJSUBUPUQUCUKUDAOUIUNUJSUETUFUGTUH $.
      $( [22-Oct-2003] $)
  $}

  ${
    $d A f g y z $.  $d A h $.  $d A f x y z $.  $d w x y z $.
    $( Well-ordering theorem: any set ` A ` can be well-ordered.  This is an
       equivalent of the Axiom of Choice.  Theorem 6 of [Suppes] p. 242.  First
       proved by Ernst Zermelo (the "Z" in ZFC) in 1904.  (Contributed by Mario
       Carneiro, 5-Jan-2013.) $)
    weth $p |- ( A e. V -> E. x x We A ) $=
      ( vy vz vw cv wwe wex wceq weeq2 exbidv wal c0 wne cfv wcel wi wral ac4
      ax-gen aceq8 mpbir a4i vtoclg ) DGZAGZHZAIZBUGHZAIDBCUFBJUHUJAUFBUGKLUIDU
      IDMEGZNOUKFGPUKQREUFSFIZDMULDDEFTUADEFAUBUCUDUE $.
      $( [5-Jan-2013] $)
  $}

  ${
    $d x y w h t z f g u v r s a b A $.  $d h t f B $.
    $d x y z v u f g h t r s a b F $.  $d h t f G $.  $d t C $.
    $d y u v f t a b D $.  $d x y z w g u v f t s r a b R $.
    $d x y u v w f t s r a b $.
    zorn2lem.1 $e |- A e. _V $.
    $( Hypotheses for transfinite recursion. $)
    zorn2lem.2 $e |- B = { f | E. h e. On ( f Fn h /\
                A. t e. h ( f ` t ) = ( G ` ( f |` t ) ) ) } $.
    zorn2lem.3 $e |- F = U. B $.
    $( Auxiliary sets. $)
    zorn2lem.4 $e |- C = { z e. A | A. g e. ran f g R z } $.
    zorn2lem.5 $e |- D = { z e. A | A. g e. ( F " x ) g R z } $.
    $( Generator function.  ` w ` is a well-ordering on ` A ` . $)
    zorn2lem.6 $e |- G =
                { <. f , t >. | t = U. { v e. C | A. u e. C -. u w v } } $.
    $( Lemma for ~ zorn2 . $)
    zorn2lem1 $p |- ( ( x e. On /\ ( w We A /\ D =/= (/) ) ) ->
                   ( F ` x ) e. D ) $=
      ( cv con0 wcel wwe c0 wne wa cfv wbr wn wral crab cuni wss ssrab2 eqsstri
      cima ssexi wereucl mp3an2 cres tfr2 wceq copab wfun cvv tfrlem7 resfunexg
      fveq1i vex mp2an rabex cab crn rneq df-ima syl6eqr eleq2d imbi1d ralbidv2
      rabbidv 3eqtr4g anbi12d abbidv df-rab unieqd fvopab syl6eq eleq1d syl5ibr
      uniex eqtri imp ) AUCZUDUEZGCUCZUFZJUGUHZUIZWPOUJZJUEZXAXCWQEUCZDUCZWRUKU
      LZEJUMZDJUNZUOZJUEZWSJGUPWTXJJMUCZBUCKUKZMOWPUSZUMZBGUNZGUAXNBGUQURZDEGJW
      RJGQXPUTZVAVBWQXBXIJWQXBOWPVCZPUJZXINFAHLOPRSVDXSXRFUCXFEIUMZDIUNZUOZVELF
      VFZUJXIXRPYCUBVKLFXRYBXIOVGWPVHUEXRVHUENFHLOPRSVIAVLOWPVHVJVMXHXGDJXQVNWM
      LUCZXRVEZYAXHYEXEIUEZXTUIZDVOXEJUEZXGUIZDVOYAXHYEYGYIDYEYFYHXTXGYEIJXEYEX
      LMYDVPZUMZBGUNXOIJYEYKXNBGYEXLXLMYJXMYEXKYJUEXKXMUEXLYEYJXMXKYEYJXRVPXMYD
      XRVQOWPVRVSVTWAWBWCTUAWDZVTYEXFXFEIJYEXDIUEXDJUEXFYEIJXDYLVTWAWBWEWFXTDIW
      GXGDJWGWDWHWIWNWJWKWLWO $.
      $( [3-Apr-1997] $)

    $( Lemma for ~ zorn2 . $)
    zorn2lem2 $p |- ( ( x e. On /\ ( w We A /\ D =/= (/) ) ) ->
                   ( y e. x -> ( F ` y ) R ( F ` x ) ) ) $=
      ( cv con0 wcel wwe c0 wne cfv cima wbr wfun cdm wss tfrlem7 onss wfn wceq
      wa wi tfr1 fndm syl6sseqr funfvima2 sylancr adantr wral zorn2lem1 ralbidv
      ax-mp breq2 elrab2 simprbi breq1 rcla4cv 3syl syld ) AUDZUEUFZHDUDUGKUHUI
      UTZUTZBUDZVSUFZWCPUJZPVSUKZUFZWEVSPUJZLULZVTWDWGVAZWAVTPUMVSPUNZUOWJOGIMP
      QSTUPVTVSUEWKVSUQPUEURWKUEUSOGIMPQSTVBUEPVCVKVDVSWCPVEVFVGWBWHKUFZNUDZWHL
      ULZNWFVHZWGWIVAACDEFGHIJKLMNOPQRSTUAUBUCVIWLWHHUFWOWMCUDZLULZNWFVHWOCWHHK
      WPWHUSWQWNNWFWPWHWMLVLVJUBVMVNWNWINWEWFWMWEWHLVOVPVQVR $.
      $( [3-Apr-1997] $)

    $( Lemma for ~ zorn2 . $)
    zorn2lem3 $p |- ( ( R Po A /\ ( x e. On /\ ( w We A /\ D =/= (/) ) ) ) ->
                   ( y e. x -> -. ( F ` x ) = ( F ` y ) ) ) $=
      ( wpo cv con0 wcel wwe c0 wne wa cfv wbr wn wi zorn2lem2 adantl zorn2lem1
      wceq cima wral crab ssrab2 eqsstri sseli breq1 biimprcd poirr nsyli com12
      syl sylan2 syld ) HLUDZAUEZUFUGHDUEUHKUIUJUKUKZUKBUEZVOUGZVQPULZVOPULZLUM
      ZVTVSUSZUNZVPVRWAUOVNABCDEFGHIJKLMNOPQRSTUAUBUCUPUQVPVNVTHUGZWAWCUOVPVTKU
      GWDACDEFGHIJKLMNOPQRSTUAUBUCURKHVTKNUECUELUMNPVOUTVAZCHVBHUBWECHVCVDVEVKW
      AVNWDUKZWCWAWBVTVTLUMZWFWBWGWAVTVSVTLVFVGHVTLVHVIVJVLVM $.
      $( [3-Apr-1997] $)

    $( Lemma for ~ zorn2 . $)
    zorn2lem4 $p |- ( ( R Po A /\ w We A ) -> E. x e. On D = (/) ) $=
      ( vy wpo cv wwe wa c0 wceq con0 wrex crn cvv wcel wn pm3.24 wne wal df-ne
      wi wral ralbii df-ral ralnex 3bitr3i wss cfv wfn tfr1 fvelrnb ax-mp ax-17
      wb hba1 hban zorn2lem1 wbr cima crab ssrab2 eqsstri sseli eleq1 syl5ibcom
      syl exp32 com12 a2d a4sd imp rexlimd syl5bi ssex ex adantl ccnv zorn2lem3
      ssrdv wfun exp45 com23 imp4a alrimdv alimdv r2al cres ssid tz7.48lem mpan
      syl6ibr wrel tfrlem6 fndm eqimssi relssres mp2an cnveqi funeq sylib onprc
      cdm syl6 funrnex df-rn eleq1i dfdm4 eqtr3i 3imtr4g mtoi jcad syl5bir mt3i
      ) GKUDZGCUEUFZUGZJUHUIZAUJUKZOULZUMUNZYSUOZUGZYSUPYQUOZAUEZUJUNZJUHUQZUTZ
      AURZYOUUAUUEAUJVAYPUOZAUJVAUUGUUBUUEUUHAUJJUHUSVBUUEAUJVCYPAUJVDVEYOUUGYS
      YTYNUUGYSUTYMYNUUGYSYNUUGUGZYRGVFYSUUIUCYRGUCUEZYRUNZUUCOVGZUUJUIZAUJUKZU
      UIUUJGUNZOUJVHZUUKUUNVMNFHLOPRSVIZAUJUUJOVJVKUUIUUMUUOAUJYNUUGAYNAVLUUFAV
      NVOUUOAVLYNUUGUUDUUMUUOUTZUTZYNUUFUUSAYNUUDUUEUURUUDYNUUEUURUTUUDYNUUEUUR
      UUDYNUUEUGUGZUULGUNZUUMUUOUUTUULJUNUVAABCDEFGHIJKLMNOPQRSTUAUBVPJGUULJMUE
      BUEKVQMOUUCVRVAZBGVSGUAUVBBGVTWAWBWEUULUUJGWCWDWFWGWHWIWJWKWLWRYRGQWMWEWN
      WOYOUUGOWPZWSZYTYOUUGUULUUJOVGUIUOZUCUUCVAAUJVAZUVDYOUUGUUDUUJUUCUNZUGUVE
      UTZUCURZAURUVFYOUUFUVIAYOUUFUVHUCYOUUFUUDUVGUVEYOUUDUUEUVGUVEUTZYMYNUUDUU
      EUVJUTZUTYMUUDYNUVKYMUUDYNUUEUVJAUCBCDEFGHIJKLMNOPQRSTUAUBWQWTXAWJWHXBXCX
      DUVEAUCUJUUCXEXJUVFOUJXFZWPZWSZUVDUJUJVFUVFUVNUJXGAUCUJOUUQXHXIUVMUVCUIUV
      NUVDVMUVLOOXKOYAZUJVFUVLOUINFHLOPRSXLUVOUJUUPUVOUJUIUUQUJOXMVKZXNOUJXOXPX
      QUVMUVCXRVKXSYBUVDYSUJUMUNZXTUVDUVCYAZUMUNZUVCULZUMUNZYSUVQUVSUVDUWAUMUVC
      YCWGYRUVRUMOYDYEUJUVTUMUVOUJUVTUVPOYFYGYEYHYIYBYJYKYL $.
      $( [3-Apr-1997] $)

    ${
      $d x u v f t s r a b H $.
      $( Auxiliary set.  ` H ` is ` D ` with ` x ` substituted for ` y ` . $)
      zorn2lem.7 $e |- H = { z e. A | A. g e. ( F " y ) g R z } $.
      $( Lemma for ~ zorn2 . $)
      zorn2lem5 $p |- ( ( ( w We A /\ x e. On ) /\ A. y e. x H =/= (/) ) ->
            ( F " x ) C_ A ) $=
        ( vs cv wwe con0 wcel wa c0 wne wral cima cfv wceq wrex tfrlem7 fvelima
        wfun mpan ax-17 hbra1 hban wal df-ral onelon zorn2lem1 wbr crab eqsstri
        wi ssrab2 sseli syl eleq1 syl5ib sylani com12 exp43 imp a2d a4sd syl5bi
        com3r rexlimd syl5 ssrdv ) HDUGUHZAUGZUIUJZUKZRULUMZBWKUNZUKZUFPWKUOZHU
        FUGZWQUJZBUGZPUPZWRUQZBWKURZWPWRHUJZPVAWSXCOGIMPQTUAUSBWRWKPUTVBWPXBXDB
        WKWMWOBWMBVCWNBWKVDVEXDBVCWMWOWTWKUJZXBXDVMZVMZWOXEWNVMZBVFWMXGWNBWKVGW
        MXHXGBWMXEWNXFWJWLXEWNXFVMZVMWLXEWJXIWLXEWJWNXFXBWLXEUKZWJWNUKZUKXDXJXB
        WTUIUJZXKXDWKWTVHXLXKUKZXAHUJZXBXDXMXARUJXNBCDEFGHIJRLMNOPQSTUAUBUEUDVI
        RHXARNUGCUGLVJNPWTUOUNZCHVKHUEXOCHVNVLVOVPXAWRHVQVRVSVTWAWFWBWCWDWEWBWG
        WHWI $.
        $( [4-Apr-1997] $)

      $( Lemma for ~ zorn2 . $)
      zorn2lem6 $p |- ( R Po A ->
   ( ( ( w We A /\ x e. On ) /\ A. y e. x H =/= (/) ) -> R Or ( F " x ) ) ) $=
        ( vs vr vb va wpo cv wwe con0 wcel wa c0 wne wral cima wbr wceq w3o wor
        wss zorn2lem5 poss syl com12 wal cfv wex wfun wfn tfr1 fnfun ax-mp wrex
        wi fvelima df-rex sylib ex anim12d an4 2exbii eeanv bitri sylibr neeq1i
        crab ralbii imaeq2 raleqdv rabbidv neeq1d rcla4cv sylbi anim12i anandis
        onelon word ordtri3or syl2an eqid zorn2lem2 adantll breq12 biimpcd syl6
        eloni com23 adantrrl imp fveq2 eqeq12 syl5ib adantl adantlr wb adantrrr
        ancoms 3orim123d syl5 exp31 com4r pm2.43i exp4a com3r exlimdvv alrimivv
        a2d imp4b r2al a1i jcad df-so syl6ibr ) HLUJZHDUKULZAUKZUMUNZUOZRUPUQZB
        YTURZUOZPYTUSZLUJZUFUKZUGUKZLUTZUUHUUIVAZUUIUUHLUTZVBZUGUUFURUFUUFURZUO
        UUFLVCYRUUEUUGUUNUUEYRUUGUUEUUFHVDYRUUGVRABCDEFGHIJKLMNOPQRSTUAUBUCUDUE
        VEUUFHLVFVGVHUUEUUNVRYRUUEUUHUUFUNZUUIUUFUNZUOZUUMVRZUGVIUFVIUUNUUEUURU
        FUGUUQUHUKZYTUNZUIUKZYTUNZUOZUUSPVJZUUHVAZUVAPVJZUUIVAZUOZUOZUIVKUHVKZU
        UEUUMUUQUUTUVEUOZUHVKZUVBUVGUOZUIVKZUOZUVJPVLZUUQUVOVRPUMVMUVPOGIMPQTUA
        VNUMPVOVPUVPUUOUVLUUPUVNUVPUUOUVLUVPUUOUOUVEUHYTVQUVLUHUUHYTPVSUVEUHYTV
        TWAWBUVPUUPUVNUVPUUPUOUVGUIYTVQUVNUIUUIYTPVSUVGUIYTVTWAWBWCVPUVJUVKUVMU
        OZUIVKUHVKUVOUVIUVQUHUIUUTUVBUVEUVGWDWEUVKUVMUHUIWFWGWHUUEUVIUUMUHUIUUB
        UUDUVCUVHUUMUUDUVCNUKCUKLUTZNPUUSUSZURZCHWJZUPUQZUVRNPUVAUSZURZCHWJZUPU
        QZUOZVRZUUBUVCUVHUUMVRZVRUUDUVRNPBUKZUSZURZCHWJZUPUQZBYTURZUWHUUCUWNBYT
        RUWMUPUEWIWKUWOUUTUWBUVBUWFUWNUWBBUUSYTUWJUUSVAZUWMUWAUPUWPUWLUVTCHUWPU
        VRNUWKUVSUWJUUSPWLWMWNWOWPUWNUWFBUVAYTUWJUVAVAZUWMUWEUPUWQUWLUWDCHUWQUV
        RNUWKUWCUWJUVAPWLWMWNWOWPWCWQUUBUVCUWGUWIYSUUAUVCUWGUWIVRZVRUUAUVCYSUWR
        UUAUVCYSUWGUWIUUAUVCUUSUMUNZUVAUMUNZUOZYSUWGUOZUWIVRZUUAUVCUXAUUAUUTUVB
        UXAUUAUUTUOUWSUUAUVBUOUWTYTUUSWTYTUVAWTWRWSWBUXAUXCUXAUXBUVHUXAUUMUXAUX
        BUVHUXAUUMVRUXAUUSUVAUNZUUSUVAVAZUVAUUSUNZVBZUXAUXBUOZUVHUOZUUMUWSUUSXA
        UVAXAUXGUWTUUSXJUVAXJUUSUVAXBXCUXIUXDUUJUXEUUKUXFUULUXHUVHUXDUUJVRZUXAY
        SUWFUVHUXJVRUWBUXAYSUWFUOZUOZUXDUVHUUJUXLUXDUVDUVFLUTZUVHUUJVRUWTUXKUXD
        UXMVRUWSUIUHCDEFGHIJUWELMNOPQSTUAUBUWEXDUDXEXFUVHUXMUUJUVDUUHUVFUUILXGX
        HXIXKXLXMUVHUXEUUKVRUXHUXEUVDUVFVAUVHUUKUUSUVAPXNUVDUUHUVFUUIXOXPXQUXHU
        VHUXFUULVRZUXAYSUWBUVHUXNVRUWFUXAYSUWBUOZUOZUXFUVHUULUXPUXFUVFUVDLUTZUV
        HUULVRUWSUXOUXFUXQVRUWTUHUICDEFGHIJUWALMNOPQSTUAUBUWAXDUDXEXRUVHUXQUULU
        VGUVEUXQUULXSUVFUUIUVDUUHLXGYAXHXIXKXTXMYBYCYDYEYFXIYGYHXMYKYCYLYIYCYJU
        UMUFUGUUFUUFYMWHYNYOUFUGUUFLYPYQ $.
        $( [4-Apr-1997] $)

      $( Lemma for ~ zorn2 . $)
      zorn2lem7 $p |- ( ( R Po A /\
      A. s ( ( s C_ A /\ R Or s ) -> E. a e. A A. r e. s ( r R a \/ r = a ) ) )
                  -> E. a e. A A. b e. A -. a R b ) $=
        ( wpo cv wss wor wa wbr wceq wo wral wrex wi wal wwe wex cvv wcel ax-mp
        weth con0 wne zorn2lem4 cima crab imaeq2 raleqdv rabbidv 3eqtr4g eqeq1d
        wn c0 onminex df-ne ralbii anbi2i rexbii sylibr zorn2lem5 a1i zorn2lem6
        jcad wfun tfrlem7 vex funimaex sseq1 soeq2 anbi12d raleq rexbidv sylan9
        imbi12d cla4v adantld noel sseld 3anass potr sylan2br exp3a com23 breq1
        imp w3a biimprcd adantl exp42 sylan9r com24 imp31 alimdv df-ral 3imtr4g
        jaod a2d cbvralv breq2 ralbidv elrab eqeq1i eleq2 sylbi syl5bbr expdimp
        wb biimpd syl5bi exp32 com34 mtoi ex imp3a exp4a com4r pm2.43a reximdv2
        com4l alrimdv syl6ibr imdistand com12 adantr imp32 exp45 imp4a rexlimiv
        mpd com3l 3syl adantlr pm2.43i exlimdv mpi ) HLUJZSUKZHULZUVCLUMZUNZTUK
        ZUAUKZLUOZUVGUVHUPZUQZTUVCURZUAHUSZUTZSVAZUNZHDUKVBZDVCZUVHUBUKZLUOZVRZ
        UBHURZUAHUSZHVDVEUVRUCDHVDVGVFUVPUVQUWCDUVPUVQUWCUVPUVQUNZUWCUVBUVQUWDU
        WCUTZUVOUVBUVQUNKVSUPZAVHUSZUWFRVSVIZBAUKZURZUNZAVHUSZUWEACDEFGHIJKLMNO
        PQUCUDUEUFUGUHVJUWGUWFRVSUPZVRZBUWIURZUNZAVHUSUWLUWFUWMABUWIBUKZUPZKRVS
        UWRNUKZCUKZLUOZNPUWIVKZURZCHVLZUXANPUWQVKZURZCHVLKRUWRUXCUXFCHUWRUXANUX
        BUXEUWIUWQPVMVNVOUGUIVPVQVTUWKUWPAVHUWJUWOUWFUWHUWNBUWIRVSWAWBWCWDWEUWK
        UWEAVHUWDUWIVHVEZUWKUWCUWDUXGUWFUWJUWCUVPUVQUXGUWFUWJUWCUTZUTUVPUWFUVQU
        XGUNZUXHUVPUWFUXIUWJUWCUVPUWFUXIUWJUNZUNZUNUVKTUXBURZUAHUSZUWCUVPUXKUXM
        UVPUXJUXMUWFUVBUXJUXBHULZUXBLUMZUNZUVOUXMUVBUXJUXNUXOUXJUXNUTUVBABCDEFG
        HIJKLMNOPQRUCUDUEUFUGUHUIWFZWGABCDEFGHIJKLMNOPQRUCUDUEUFUGUHUIWHWIUVNUX
        PUXMUTSUXBPWJUXBVDVEOGIMPQUDUEWKPUWIAWLWMVFUVCUXBUPZUVFUXPUVMUXMUXRUVDU
        XNUVEUXOUVCUXBHWNUVCUXBLWOWPUXRUVLUXLUAHUVKTUVCUXBWQWRWTXAWSXBXKUVPUWFU
        XJUXMUWCUTZUVBUWFUXJUXSUTZUTUVOUWFUVBUXTUWFUVBUXJUXSUWFUVBUXJUNZUNZUXLU
        WBUAHHUYBUVHHVEZUXLUWBUYBUYCUXLUWBUYBUYCUXLUNZUVSHVEZUWAUTZUBVAUWBUYBUY
        DUYFUBUYBUYCUXLUYFUYEUYBUYCUXLUWAUYEUWFUYAUYCUXLUWAUTZUTZUWFUYEUYAUYHUT
        UWFUYEUYAUYEUYHUWFUYEUYAUYEUYHUTUTUWFUYEUNZUYAUYCUYEUYGUYIUYAUYCUYEUYGU
        YIUYAUYCUYEUNZUXLUWAUYIUYAUYJUNZUNUXLUNUVTUVSVSVEZUVSXCUYIUYKUXLUVTUYLU
        TUYIUYKUVTUXLUYLUYIUYKUVTUXLUYLUTUYKUVTUNZUXLUVGUVSLUOZTUXBURZUYIUYLUYM
        UVGUXBVEZUVKUTZTVAUYPUYNUTZTVAUXLUYOUYMUYQUYRTUYMUYPUVKUYNUYAUYJUVTUYPU
        VKUYNUTZUTZUYAUVTUYJUYTUYAUYPUYJUVTUYSUXJUYPUVGHVEZUVBUYJUVTUYSUTUTUXJU
        XBHUVGUXQXDUVBVUAUYJUVTUYSUVBVUAUYJUNZUNZUVTUNUVIUYNUVJVUCUVTUVIUYNUTVU
        CUVIUVTUYNVUCUVIUVTUYNVUBUVBVUAUYCUYEXLUVIUVTUNUYNUTVUAUYCUYEXEHUVGUVHU
        VSLXFXGXHXIXKUVTUVJUYNUTVUCUVJUYNUVTUVGUVHUVSLXJXMXNYBXOXPXQXIXRYCXSUVK
        TUXBXTUYNTUXBXTYAUYOUWSUVSLUOZNUXBURZUYIUYLUYNVUDTNUXBUVGUWSUVSLXJYDUWF
        UYEVUEUYLUWFUYEVUEUNZUYLVUFUVSUXDVEZUWFUYLUXCVUECUVSHUWTUVSUPUXAVUDNUXB
        UWTUVSUWSLYEYFYGUWFUXDVSUPVUGUYLYMKUXDVSUGYHUXDVSUVSYIYJYKYNYLYOXPYPYQX
        RYRXOUUAYQYSUUBUUCYTUUEYTUUFUWAUBHXTUUGXHUUHUUDYPUUIUUJUUKUUOUULXIYLUUM
        UUPUUNUUQUURUUSYSUUTUVA $.
        $( [6-Apr-1997] $)
    $}

  $}

  ${
    $d x y z $.  $d x y w $.
    $( Lemma for ~ zorn . $)
    zornlem $p |- ( z { <. x , y >. | x C. y } w <-> z C. w ) $=
      ( cv wpss copab vex psseq1 psseq2 eqid brab ) AEZBEZFZCEZNFPDEZFABPQOABGZ
      CHDHMPNINQPJRKL $.
      $( [13-Aug-2004] $)
  $}

  ${
    $d x y z w v u f g h t s r q a b c d j k m n R $.
    $d x y z w v u f g h t s r q a b c d j k m n A $.
    zorn2.1 $e |- A e. _V $.
    $( Zorn's Lemma of [Monk1] p. 117.  This theorem is equivalent to the Axiom
       of Choice and states that every partially ordered set ` A ` (with an
       ordering relation ` R ` ) in which every totally ordered subset has an
       upper bound, contains at least one maximal element.  The main proof
       consists of lemmas ~ zorn2lem1 through ~ zorn2lem7 ; this final piece
       mainly changes bound variables to eliminate the hypotheses of
       ~ zorn2lem7 . $)
    zorn2 $p |- ( ( R Po A /\
      A. w ( ( w C_ A /\ R Or w ) -> E. x e. A A. z e. w ( z R x \/ z = x ) ) )
                  -> E. x e. A A. y e. A -. x R y ) $=
      ( vr vq vn vj vk vm vv vs cv wbr wral crab wceq vt vu vg va vb vc wfn cfv
      vh vd vf cres wn crn cuni copab con0 wrex cima rdglem1 eqid breq1 cbvralv
      wa cab breq2 ralbidv syl5bb cbvrabv wcel eleq1 notbid anbi12d cbvabv rneq
      id raleqdv rabbidv eleq2d wb raleq abbidv syl5eq df-rab unieqd eqeqan12rd
      syl 3eqtr4g cbvopabv zorn2lem7 ) UAUBHIJKUCEUDPZUEPZUGUFPZWKUHWKWMULLPZWN
      MPZIPZQZUMZLWPNPZFQZIUIPZUNZRZNESZRZMXDSZUOZTZUILUPZUHTUFWLRVDUEUQURUDVEZ
      WTIUJPZUNZRZNESZOPZHPZFQZOXJUOZUAPUSRHESZFUJOUKXRXIXQOXRUBPUSRHESZDCABGUE
      UFUKUCUDUJXIUTXRVAXMXQOXLRZNHEXMXOWSFQZOXLRWSXPTZYAWTYBIOXLWPXOWSFVBVCYCY
      BXQOXLWSXPXOFVFVGVHVIXSVAXHUCPZKPZJPZWPQZUMZKXNRZJXNSZUOZTUILUJUCWNYDTZXA
      XKTZWNYDXGYKYLVPYMXFYJYMWOXDVJZXEVDZMVEZYFXNVJZYIVDZJVEZXFYJYMYPYFXDVJZWN
      YFWPQZUMZLXDRZVDZJVEYSYOUUDMJWOYFTZYNYTXEUUCWOYFXDVKUUEWRUUBLXDUUEWQUUAWO
      YFWNWPVFVLVGVMVNYMUUDYRJYMYTYQUUCYIYMXDXNYFYMXCXMNEYMWTIXBXLXAXKVOVQVRZVS
      YMXDXNTZUUCYIVTUUFUUCYHKXDRUUGYIUUBYHLKXDWNYETUUAYGWNYEYFWPVBVLVCYHKXDXNW
      AVHWGVMWBWCXEMXDWDYIJXNWDWHWEWFWIXTVAWJ $.
      $( [6-Apr-1997] $)

    $( Zorn's Lemma.  If the union of every chain (with respect to inclusion)
       in a set belongs to the set, then the set contains a maximal element.
       This theorem is equivalent to the Axiom of Choice.  Theorem 6M of
       [Enderton] p. 151.  See ~ zorn2 for a version with general partial
       orderings. $)
    zorn $p |- ( A. z ( ( z C_ A /\
                  A. x e. z A. y e. z ( x C_ y \/ y C_ x ) ) -> U. z e. A ) ->
                E. x e. A A. y e. A -. x C. y ) $=
      ( vw vv vu cv wss wo wral wa wcel wi wpss wbr wn wrex zornlem wal wpo wor
      cuni copab w3a pssirr mtbir psstr sylibr syl2anb pm3.2i rgen3 df-po mpbir
      wceq a1i w3o df-so simprbi 3orbi123i sspsstri bitr4i 2ralbii sylib anim2i
      biid risset eqimss2 unissb orbi1i sspss ralbii reximi sylbi imim12i alimi
      zorn2 sylancr notbii rexbii ) CIZDJZAIZBIZJWEWDJKZBWBLAWBLZMZWBUDZDNZOZCU
      AZWDWEFIGIPFGUEZQZRZBDLZADSZWDWEPZRZBDLZADSWLDWMUBZWCWBWMUCZMZHIZWDWMQZXD
      WDUPZKZHWBLZADSZOZCUAWQXAXDXDWMQZRZXDWEWMQZWEWDWMQZMXEOZMZADLBDLHDLXPHBAD
      DDXPXDDNWEDNWDDNUFXLXOXKXDXDPXDUGFGHHTUHXMXDWEPZWEWDPZXEXNFGHBTFGBATZXQXR
      MXDWDPZXEXDWEWDUIFGHATZUJUKULUQUMHBADWMUNUOWKXJCXCWHWJXIXBWGWCXBWNWDWEUPZ
      XNURZBWBLAWBLZWGXBWBWMUBYDABWBWMUSUTYCWFABWBWBYCWRYBXRURWFWNWRYBYBXNXRFGA
      BTZYBVGXSVAWDWEVBVCVDVEVFWJWDWIUPZADSXIAWIDVHYFXHADYFXDWDJZHWBLZXHYFWIWDJ
      YHWIWDVIHWBWDVJVEXGYGHWBXGXTXFKYGXEXTXFYAVKXDWDVLVCVMUJVNVOVPVQABHCDWMEVR
      VSWPWTADWOWSBDWNWRYEVTVMWAVE $.
      $( [12-Aug-2004] $)
  $}

  ${
    $d A w x y z $.
    zornn0.1 $e |- A e. _V $.
    $( Variant of Zorn's lemma ~ zorn in which ` (/) ` , the union of the empty
       chain, is not required to be an element of ` A ` .  (Contributed by Jeff
       Madsen, 5-Jan-2011.) $)
    zornn0 $p |- ( ( A =/= (/) /\ A. z ( ( z C_ A /\ z =/= (/) /\
                  A. x e. z A. y e. z ( x C_ y \/ y C_ x ) ) -> U. z e. A ) )
                                  -> E. x e. A A. y e. A -. x C. y ) $=
      ( vw cv wss c0 wne wral cuni wcel wi wrex wa ssralv ax-mp wceq cvv wo w3a
      wal wpss wn csn cun uncom sseq2i ssundif bitri difss ralimi syl wb ssdif0
      cdif uni0b biimpri eleq1d sylbir imbi2d vex difexg sseq1 neeq1 raleqbi1dv
      raleq 3anbi123d unieq imbi12d cla4v com12 3expa an32s unidif0 elun1 sylbi
      eleq1i syl6 0ex snid elun2 a1i12 pm2.61ne sylan2 sylanb alrimiv p0ex unex
      zorn ssun1 reximi rexun simpr psseq1 0pss syl6bb notbid nne ralbidv rexsn
      eqsn biimpar sylan2b rexeqdv mpbird jaodan ) CGZDHZXIIJZAGZBGZHXMXLHUAZBX
      IKZAXIKZUBZXILZDMZNZCUCZDIJZXLXMUDZUEZBDIUFZUGZKZAYFOZYDBDKZADOZYAFGZYFHZ
      XNBYKKZAYKKZPZYKLZYFMZNZFUCYHYAYRFYOYAYQYLYKYEUQZDHZYNYAYQNZYLYKYEDUGZHYT
      YFUUBYKDYEUHUIYKYEDUJUKYNYTXNBYSKZAYSKZUUAYNYMAYSKZUUDYSYKHZYNUUENYKYEULZ
      YMAYSYKQRYMUUCAYSUUFYMUUCNUUGXNBYSYKQRUMUNYTUUDPZUUAYAIYFMZNYSIYSISZYQUUI
      YAUUJYKYEHZYQUUIUOYKYEUPUUKYPIYFYPISUUKYKURUSUTVAVBUUHYSIJZPYAYSLZDMZYQYT
      UULUUDYAUUNNZYTUULUUDUUOYAYTUULUUDUBZUUNXTUUPUUNNCYSYKTMYSTMFVCYKYETVDRXI
      YSSZXQUUPXSUUNUUQXJYTXKUULXPUUDXIYSDVEXIYSIVFXOUUCAXIYSXNBXIYSVHVGVIUUQXR
      UUMDXIYSVJUTVKVLVMVNVOUUNYPDMYQUUMYPDYKVPVSYPDYEVQVRVTUUHYAUUIIYEMUUIIWAW
      BIYEDWCRWDWEWFWGVMWHABFYFDYEEWIWJWKUNYHYBYIAYFOZYJYGYIAYFDYFHYGYINDYEWLYD
      BDYFQRWMUURYBYJYIAYEOZUAYJYIADYEWNYBYJYJUUSYBYJWOYBUUSPZYJUUSYBUUSWOUUTYI
      ADYEUUSYBXMISZBDKZDYESZYIUVBAIWAXLISZYDUVABDUVDYDXMIJZUEUVAUVDYCUVEUVDYCI
      XMUDUVEXLIXMWPXMWQWRWSXMIWTWRXAXBYBUVCUVBBDIXCXDXEXFXGXHXEWFWF $.
      $( [5-Jan-2011] $)
  $}

  ${
    $d F w y z $.  $d K w y z $.  $d g w y $.  $d s w y $.  $d w x y z $.
    axdclem.1 $e |- F = ( rec ( ( y e. _V |->
                            ( g ` { z | y x z } ) ) , s ) |` om ) $.
    $( Lemma for ~ axdc . $)
    axdclem $p |- ( ( A. y e. ~P dom x ( y =/= (/) -> ( g ` y ) e. y )
      /\ ran x C_ dom x /\ E. z ( F ` K ) x z ) ->
      ( K e. om -> ( F ` K ) x ( F ` suc K ) ) ) $=
      ( vw cv c0 wne cfv wcel wi wss wbr cab fvex wceq cdm cpw wral crn wex w3a
      csuc com brelrn abssi sstr mpan dmex elpw2 sylibr neeq1 abn0 syl6bb eleq2
      vex breq2 cbvabv eleq2i syl6bbr elab fveq2 breq2d bitrd imbi12d syl com12
      rcla4v 3imp ax17el ax-17 breq1 abbidv fveq2d frsucmpt mpan2 syl5ibrcom
      cvv ) BJZKLZWCDJZMZWCNZOZBAJZUAZUBZUCZWIUDZWJPZFEMZCJZWIQZCUEZUFWOFUGEMZW
      IQFUHNZWOWQCRZWEMZWIQZWLWNWRXCWNWLWRXCOZWNXAWKNZWLXDOWNXAWJPZXEXAWMPWNXFW
      QCWMWOWPWIFESCUTUIUJXAWMWJUKULXAWJWIAUTUMUNUOWHXDBXAWKWCXATZWDWRWGXCXGWDX
      AKLWRWCXAKUPWQCUQURXGWGWOWFWIQZXCXGWGWFWOIJZWIQZIRZNZXHXGWGWFXANXLWCXAWFU
      SXKXAWFXJWQICXIWPWOWIVAVBVCVDXJXHIWFWCWESXIWFWOWIVAVEURXGWFXBWOWIWCXAWEVF
      VGVHVIVLVJVKVMWTWSXBWOWIWTXBWBNWSXBTXAWESBIGJFWCWPWIQZCRZWEMXBWBEIGBVNXIF
      NBVOXIXBNBVOHWCWOTZXNXAWEXOXMWQCWCWOWPWIVPVQVRVSVTVGWA $.
      $( [25-Jan-2013] $)
  $}

  ${
    $d F f n $.  $d F k n y z $.  $d f g n x $.  $d g k n s y $.
    $d g k n x y z $.
    axdclem2.1 $e |- F = ( rec ( ( y e. _V |->
                             ( g ` { z | y x z } ) ) , s ) |` om ) $.
    $( Lemma for ~ axdc .  Using the full Axiom of Choice, we can construct a
       choice function ` g ` on ` ~P dom x ` .  From this, we can build a
       sequence ` F ` starting at any value ` s e. dom x ` by repeatedly
       applying ` g ` to the set ` ( F `` x ) ` (where ` x ` is the value from
       the previous iteration). $)
    axdclem2 $p |- ( E. z s x z -> ( ran x C_ dom x ->
       E. f A. n e. om ( f ` n ) x ( f ` suc n ) ) ) $=
      ( cv c0 cfv wcel wi wex wbr csuc com wceq cvv vk wne cdm cpw wral crn wss
      vex dmex pwex ac4c w3a fveq2 fveq2d breq12d weq cab cmpt crdg cres fveq1i
      suceq fr0g ax-mp eqtri breq1i biimpri eximi peano1 axdclem syl3an3 3com23
      mpi wa fvex brelrn ssel syl5 eldm syl6ib ad2antll peano2 3expia com3r imp
      syld 3adantr2 ex finds2 com12 ralrimiv wfn frfnom fneq1i mpbir omex mp2an
      fnex fveq1 ralbidv cla4ev syl 3exp exlimiv ) BJZKUBXEEJZLXEMNBAJZUCZUDZUE
      ZEOHJZCJZXGPZCOZXGUFZXHUGZFJZDJZLZXQQZXRLZXGPZFRUEZDOZNNZBXIEXHXGAUHUIUJU
      KXJYEEXJXNXPYDXJXNXPULZXQGLZXTGLZXGPZFRUEZYDYFYIFRXQRMYFYIYIKGLZKQZGLZXGP
      ZUAJZGLZYOQZGLZXGPZYRYQQZGLZXGPZYFFUAXQKSZYGYKYHYMXGXQKGUMUUCXTYLGXQKVBUN
      UOFUAUPZYGYPYHYRXGXQYOGUMUUDXTYQGXQYOVBUNUOXQYQSZYGYRYHUUAXGXQYQGUMUUEXTY
      TGXQYQVBUNUOXJXPXNYNXNXJXPYKXLXGPZCOZYNXMUUFCUUFXMYKXKXLXGYKKBTXEXLXGPCUQ
      XFLURZXKUSRUTZLZXKKGUUIIVAXKTMUUJXKSHUHXKTUUHVCVDVEVFVGVHXJXPUUGULKRMYNVI
      ABCEGKHIVJVMVKVLYORMZYFYSUUBNZUUKXJXPUULXNUUKXJXPVNZVNYSYRXLXGPCOZUUBXPYS
      UUNNUUKXJXPYSYRXHMZUUNYSYRXOMXPUUOYPYRXGYOGVOYQGVOZVPXOXHYRVQVRCYRXGUUPVS
      VTWAUUKUUMUUNUUBNUUMUUNUUKUUBXJXPUUNUUKUUBNUUKYQRMXJXPUUNULUUBYOWBABCEGYQ
      HIVJVRWCWDWEWFWGWHWIWJWKYCYJDGGRWLZRTMGTMUUQUUIRWLXKUUHWMRGUUIIWNWOWPRTGW
      RWQXRGSZYBYIFRUURXSYGYAYHXGXQXRGWSXTXRGWSUOWTXAXBXCXDVD $.
      $( [25-Jan-2013] $)
  $}

  ${
    $d f g n u v x z $.  $d f n u v w x z $.  $d f g n v x y z $.
    $( This theorem derives ~ ax-dc using ~ ax-ac and ~ ax-inf .  Thus, _AC_
       implies _DC_, but not vice-versa (so that ZFC is strictly stronger than
       ZF+DC).  (Contributed by Mario Carneiro, 25-Jan-2013.) $)
    axdc $p |- ( ( E. y E. z y x z /\ ran x C_ dom x ) ->
       E. f A. n e. om ( f ` n ) x ( f ` suc n ) ) $=
      ( vv vg vu vw cv wbr wex cfv com cvv cab cmpt crdg cres wceq crn cdm csuc
      wss wral wi wcel ax-17 weq breq2 cbvabv breq1 abbidv syl5eq fveq2d cbvmpt
      rdgeq1 reseq1 mp2b axdclem2 exlimiv imp ) BJZCJZAJZKCLZBLVEUAVEUBUDZEJZDJ
      ZMVHUCVIMVEKENUEDLZVFVGVJUFBAFCDGEHOHJZIJZVEKZIPZGJZMZQZVCRZNSZBVQFOFJZVD
      VEKZCPZVOMZQZTVRWDVCRZTVSWENSTHFDOVPWCVIVPUGFUHVIWCUGHUHHFUIZVNWBVOWFVNVK
      VDVEKZCPWBVMWGICVLVDVKVEUJUKWFWGWACVKVTVDVEULUMUNUOUPVCVQWDUQVRWENURUSUTV
      AVB $.
      $( [25-Jan-2013] $)
  $}

  ${
    $d f F $.  $d f A $.  $d f B $.
    fodom.1 $e |- A e. _V $.
    $( An onto function implies dominance of domain over range.  Lemma 10.20 of
       [Kunen] p. 30.  This theorem uses the Axiom of Choice ~ ac7g .  AC is
       not needed for finite sets - see ~ fodomfi .  See also ~ fodomnum . $)
    fodom $p |- ( F : A -onto-> B -> B ~<_ A ) $=
      ( vf ccnv wss cdm wfn wa cdom wbr cvv wcel crn syl5eqr wfun adantlr mpsyl
      wf wfo cv wex fof fex sylancl cnvexg ac7g 3syl df-rn forn fneq2d wf1 rnex
      vex dffn3 biimpi ad2antlr fofun cnvss cnvcnvss syl6ss funss impcom syl2an
      wi df-f1 sylanbrc f1dom2g rnss adantl dfdm4 fdm syl adantr sseqtrd ssdomg
      wceq domtr syl2anc exp31 sylbid com23 imp3a exlimdv mpd ) ABCUAZEUBZCFZGZ
      WHWIHZIZJZEUCZBAKLZWGCMNZWIMNWNWGABCTZAMNWPABCUDZDABMCUEUFCMUGMWIEUHUIWGW
      MWOEWGWJWLWOWGWLWJWOWGWLWHBIZWJWOVFWGWKBWHWGWKCOBCUJABCUKPULWGWSWJWOWGWSJ
      WJJZBWHOZKLZXAAKLZWOXAMNZWTBXAWHUMZXBWHEUOUNZWTBXAWHTZWHFZQZXEWSXGWGWJWSX
      GBWHUPUQURWGWJXIWSWGCQZXHCGZXIWJABCUSWJXHWIFCWHWIUTCVAVBXKXJXIXHCVCVDVERB
      XAWHVGVHBXAMWHVISWGWJXCWSXDWGWJJZXAAGXCXFXLXAWIOZAWJXAXMGWGWHWIVJVKWGXMAV
      RWJWGXMCHZACVLWGWQXNAVRWRABCVMVNPVOVPXAAMVQSRBXAAVSVTWAWBWCWDWEWF $.
      $( [23-Jul-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.
    $( An onto function implies dominance of domain over range. $)
    fodomg $p |- ( A e. C -> ( F : A -onto-> B -> B ~<_ A ) ) $=
      ( vx cv wfo cdom wbr wi wceq foeq2 breq2 imbi12d vex fodom vtoclg ) EFZBD
      GZBRHIZJABDGZBAHIZJEACRAKSUATUBRABDLRABHMNRBDEOPQ $.
      $( [23-Jul-2004] $)
  $}

  ${
    $d f g z A $.  $d f g z B $.
    fodomb.1 $e |- A e. _V $.
    $( Equivalence of an onto mapping and dominance for a non-empty set.
       Proposition 10.35 of [TakeutiZaring] p. 93. $)
    fodomb $p |- ( ( A =/= (/) /\ E. f f : A -onto-> B ) <->
                 ( (/) ~< B /\ B ~<_ A ) ) $=
      ( c0 wne cv wfo wex wa csdm wbr cdom wceq syl eqeq1d cvv wcel wi ax-mp wf
      cdm fof fdm crn dm0rn0 forn syl5bb bitr3d necon3bid biimpac fornex 0sdomg
      wb adantl mpbird fodom a1i jcad exlimdv imp sdomdomtr 0sdom fodomr mp3an1
      ex sylib jca impbii ) AEFZABCGZHZCIZJEBKLZBAMLZJZVJVMVPVJVLVPCVJVLVNVOVJV
      LVNVJVLJVNBEFZVLVJVQVLAEBEVLVKUBZENZAENBENZVLVRAEVLABVKUAVRANABVKUCABVKUD
      OPVSVKUEZENVLVTVKUFVLWABEABVKUGPUHUIUJUKVLVNVQUNZVJVLBQRZWBAQRZVLWCSDABQV
      KULTBQUMOUOUPVFVLVOSVJABVKDUQURUSUTVAVPVJVMVPEAKLZVJWDVPWESDEBAQVBTADVCVG
      WDVNVOVMDABQCVDVEVHVI $.
      $( [24-Feb-2007] $) $( [29-Jul-2004] $)
  $}

  ${
    $d f x y A $.  $d f x y B $.
    brdom3.1 $e |- A e. _V $.
    brdom3.2 $e |- B e. _V $.
    $( Equivalence to a dominance relation. $)
    brdom3 $p |- ( A ~<_ B <-> E. f ( A. x E* y x f y /\
                 A. x e. A E. y e. B y f x ) ) $=
      ( cdom wbr cv wmo wal wa wex c0 wceq cvv ax-mp wrel wss wrex wfo wo wn wi
      wral csdm wne 0sdom df-ne bitr2i wcel fodomr mp3an1 sylanb pm5.6 mpbi cop
      ancoms noel df-br mtbir nex exmo ori ax-gen rzal ax-17 breq mobid rexbidv
      0ex albidv ralbidv anbi12d cla4ev sylancr wfun fofun dffun6 simprbi dffo4
      syl wf jca eximi cxp cin cdm wfn crn inss1 ssbri immoi alimi relxp relin2
      jaoi mpbiran sylibr funfn sylib rninxp biimpri df-fo vex inex1 dmex fodom
      anim12i inss2 dmss dmxpss sstri ssdom2g domtr mpan2 3syl exlimiv impbii
      mp2 ) CDHIZAJZBJZEJZIZBKZALZYDYCYEIZBDUAZACUFZMZENZYBCOPZDCYEUBZENZUCZYMY
      BYNUDZMYPUEYBYQUEYRYBYPYROCUGIZYBYPYSCOUHYRCFUICOUJUKDQULZYSYBYPGDCQEUMUN
      UOUSYBYNYPUPUQYNYMYPYNYCYDOIZBKZALZYDYCOIZBDUAZACUFZYMUUBAUUABNZUDUUBUUAB
      UUAYCYDURZOULUUHUTYCYDOVAVBVCUUGUUBUUABVDVERVFUUEACVGYLUUCUUFMEOVLYEOPZYH
      UUCYKUUFUUIYGUUBAUUIYFUUABUUIBVHYCYDYEOVIVJVMUUIYJUUEACUUIYIUUDBDYDYCYEOV
      IVKVNVOVPVQYOYLEYOYHYKYOYEVRZYHDCYEVSUUJYESYHABYEVTWAWCYODCYEWDYKBADCYEWB
      WAWEWFWRWCYLYBEYLYEDCWGZWHZWIZCUULUBZCUUMHIZYBYLUULUUMWJZUULWKCPZMUUNYHUU
      PYKUUQYHUULVRZUUPYHYCYDUULIZBKZALZUURYGUUTAUUSYFBUULYEYCYDYEUUKWLWMWNWOUU
      RUULSZUVAABUULVTUUKSUVBDCWPYEUUKWQRWSWTUULXAXBUUQYKBADCYEXCXDXJUUMCUULXEW
      TUUMCUULUULYEUUKEXFXGXHXIUUOUUMDHIZYBYTUUMDTUVCGUUMUUKWIZDUULUUKTUUMUVDTY
      EUUKXKUULUUKXLRDCXMXNUUMDQXOYACUUMDXPXQXRXSXT $.
      $( [27-Mar-2007] $)
  $}

  ${
    $d f g x y A v w z $.  $d f g x y B v w z $.
    brdom4.1 $e |- A e. _V $.
    brdom4.2 $e |- B e. _V $.
    $( An equivalence to a dominance relation. $)
    brdom5 $p |- ( A ~<_ B <->
  E. f ( A. x e. B E* y x f y /\ A. x e. A E. y e. B y f x ) ) $=
      ( cdom wbr cv wmo wral wa wex cdm wrel wcel wss ax-mp sylibr brdom3 alral
      wrex wal anim1i eximi sylbi cxp cin wfo wceq wfun inss2 dmss dmxpss sstri
      wfn crn sseli inss1 ssbri immoi imim12i ralimi2 relxp relin2 jctil dffun7
      funfn sylib rninxp biimpri anim12i df-fo vex inex1 dmex fodom cvv ssdom2g
      mp2 domtr mpan2 3syl exlimiv impbii ) CDHIZAJZBJZEJZIZBKZADLZWIWHWJIBDUCA
      CLZMZENZWGWLAUDZWNMZENWPABCDEFGUAWRWOEWQWMWNWLADUBUEUFUGWOWGEWOWJDCUHZUIZ
      OZCWTUJZCXAHIZWGWOWTXAUQZWTURCUKZMXBWMXDWNXEWMWTULZXDWMWTPZWHWIWTIZBKZAXA
      LZMXFWMXJXGWLXIADXAWHXAQWHDQWLXIXADWHXAWSOZDWTWSRXAXKRWJWSUMWTWSUNSDCUOUP
      ZUSXHWKBWTWJWHWIWJWSUTVAVBVCVDWSPXGDCVEWJWSVFSVGABWTVHTWTVIVJXEWNBADCWJVK
      VLVMXACWTVNTXACWTWTWJWSEVOVPVQVRXCXADHIZWGDVSQXADRXMGXLXADVSVTWACXADWBWCW
      DWEWF $.
      $( [29-Mar-2007] $)

    $( An equivalence to a dominance relation. $)
    brdom4 $p |- ( A ~<_ B <->
  E. f ( A. x e. B E* y ( y e. A /\ x f y ) /\ A. x e. A E. y e. B y f x ) ) $=
      ( cdom wbr cv wcel wa wmo wral wex wal cdm crn wss ax-mp wrex brdom3 moan
      alimi alral syl anim1i eximi sylbi cxp cin wfo wfn wceq wfun inss2 dmxpss
      wrel dmss sstri sseli rnss rnxpss inss1 ssbri anim12i immoi imim12i relxp
      ralimi2 relin2 jctil dffun9 sylibr funfn sylib rninxp biimpri df-fo inex1
      vex dmex fodom cvv ssdom2g mp2 domtr mpan2 3syl exlimiv impbii ) CDHIZBJZ
      CKZAJZWMEJZIZLZBMZADNZWMWOWPIBDUAACNZLZEOZWLWQBMZAPZXALZEOXCABCDEFGUBXFXB
      EXEWTXAXEWSAPWTXDWSAWQWNBUCUDWSADUEUFUGUHUIXBWLEXBWPDCUJZUKZQZCXHULZCXIHI
      ZWLXBXHXIUMZXHRZCUNZLXJWTXLXAXNWTXHUOZXLWTXHURZWMXMKZWOWMXHIZLZBMZAXINZLX
      OWTYAXPWSXTADXIWOXIKWODKWSXTXIDWOXIXGQZDXHXGSZXIYBSWPXGUPZXHXGUSTDCUQUTZV
      AXSWRBXQWNXRWQXMCWMXMXGRZCYCXMYFSYDXHXGVBTDCVCUTVAXHWPWOWMWPXGVDVEVFVGVHV
      JXGURXPDCVIWPXGVKTVLABXHVMVNXHVOVPXNXABADCWPVQVRVFXICXHVSVNXICXHXHWPXGEWA
      VTWBWCXKXIDHIZWLDWDKXIDSYGGYEXIDWDWEWFCXIDWGWHWIWJWK $.
      $( [28-Mar-2007] $)
  $}

  ${
    $d f g x y A v w z $.  $d f g x y B v w z $.
    brdom7disj.1 $e |- A e. _V $.
    brdom7disj.2 $e |- B e. _V $.
    brdom7disj.3 $e |- ( A i^i B ) = (/) $.
    $( An equivalence to a dominance relation for disjoint sets. $)
    brdom7disj $p |- ( A ~<_ B <-> E. f ( A. x e. B E* y
      ( y e. A /\ { x , y } e. f ) /\ A. x e. A E. y e. B { y , x } e. f ) ) $=
      ( vv vz vw wbr cv wcel wa wral wrex cpr wceq cab vg cdom wmo brdom4 ax-17
      wex cop wb wne cin c0 incom eqtri disjne mp3an1 vex opthpr equcom anbi12i
      syl syl6rbb df-br anbi12d rexbidva rexbidv rexcom zfpair2 anbi1d 2rexbidv
      a1i eqeq1 elab bitr4i adantr breq1 breq2 ceqsrex2v bitrd pm5.32da ralbiia
      mobid ancoms eqcom ancom 3bitr4g bicomi syl5bb snex simpl ss2abi sseqtr4i
      csn df-sn ssexi abrexex2 eleq2 anbi2d ralbidv cla4ev syl2anbr copab preq1
      exlimiv eleq1d preq2 eqid brab anbi2i mobii ralbii cvv df-opab cuni uniex
      rexbii prid1 elunii mpan adantl prid2 eqeltrri abexex eqeltri breq impbii
      bitri ) CDUBLBMZCNZAMZYGUAMZLZOZBUCZADPZYGYIYJLZBDQZACPZOZUAUFZYHYIYGRZEM
      ZNZOZBUCZADPZYGYIRZUUANZBDQZACPZOZEUFZABCDUAFGUDYSUUKYRUUKUAYNYHYTIMZJMZK
      MZRZSZUUMUUNUGYJNZOZJDQZKCQZITZNZOZBUCZADPZUUFUVANZBDQZACPZUUKYQUVDYMADYI
      DNZUVCYLBUVIBUEUVIYHUVBYKUVIYHOUVBUUMYISZUUNYGSZOZUUMUUNYJLZOZKCQZJDQZYKU
      VIUVBUVPUHYHUVIUVPYTUUOSZUUQOZKCQZJDQZUVBUVIUVOUVSJDUVIUVNUVRKCUVIUUNCNZO
      ZUVLUVQUVMUUQUWBUVQYIUUMSZYGUUNSZOZUVLUWBYIUUNUIZUVQUWEUHDCUJZUKSZUVIUWAU
      WFUWGCDUJUKDCULHUMZDCYIUUNUNUOYIYGUUMUUNAUPZBUPZJUPZKUPZUQUTUWCUVJUWDUVKA
      JURBKURUSVAUVMUUQUHUWBUUMUUNYJVBZVJVCVDVEUVTUVRJDQKCQZUVBUVRJKDCVFUUTUWOI
      YTABVGUULYTSZUURUVRKJCDUWPUUPUVQUUQUULYTUUOVKVHVIVLVMVAVNUVMYIUUNYJLYKJKY
      IYGDCUUMYIUUNYJVOUUNYGYIYJVPVQVRVSWAVTUVGYPACYICNZUVFYOBDUWQYGDNZOUVFUUNY
      ISZUUMYGSZOZUVMOZJDQZKCQZYOUWQUVFUXDUHUWRUVFUUFUUOSZUUQOZJDQZKCQZUWQUXDUU
      TUXHIUUFBAVGUULUUFSZUURUXFKJCDUXIUUPUXEUUQUULUUFUUOVKVHVIVLUWQUXGUXCKCUWQ
      UXFUXBJDUWQUUMDNZOZUXEUXAUUQUVMUXKUUOUUFSZUWTUWSOZUXEUXAUXKUUMYIUIZUXLUXM
      UHUXJUWQUXNUWHUXJUWQUXNUWIDCUUMYIUNUOWBUUMUUNYGYIUWLUWMUWKUWJUQUTUUFUUOWC
      UWSUWTWDWEUUQUVMUHUXKUVMUUQUWNWFVJVCVDVEWGVNUVMUUMYIYJLYOKJYIYGCDUUNYIUUM
      YJVPUUMYGYIYJVOVQVRVDVTUUJUVEUVHOEUVAUUSKICFUURJIDGUURITZUUOWLZUUOWHUXOUU
      PITUXPUURUUPIUUPUUQWIWJIUUOWMWKWNWOWOUUAUVASZUUEUVEUUIUVHUXQUUDUVDADUXQUU
      CUVCBUXQBUEUXQUUBUVBYHUUAUVAYTWPWQWAWRUXQUUHUVGACUXQUUGUVFBDUUAUVAUUFWPVE
      WRVCWSWTXCUUJYSEUUEYHYIYGUUNUUMRZUUANZKJXAZLZOZBUCZADPZYGYIUXTLZBDQZACPZY
      SUUIUYCUUDADUYBUUCBUYAUUBYHUXSYIUUMRZUUANUUBKJYIYGUXTUWJUWKUWSUXRUYHUUAUU
      NYIUUMXBXDUWTUYHYTUUAUUMYGYIXEXDUXTXFZXGXHXIXJUYFUUHACUYEUUGBDUXSYGUUMRZU
      UANUUGKJYGYIUXTUWKUWJUVKUXRUYJUUAUUNYGUUMXBXDUVJUYJUUFUUAUUMYIYGXEXDUYIXG
      XOXJYRUYDUYGOUAUXTUXTUULUUNUUMUGZSZUXSOZJUFZKUFITXKUXSKJIXLUYNKIUUAXMZUUA
      EUPXNZUYMUUNUYONZJUXSUYQUYLUUNUXRNUXSUYQUUNUUMUWMXPUUNUXRUUAXQXRXSXCUYMJI
      UYOUYPUXSUUMUYONZUYLUUMUXRNUXSUYRUUNUUMUWLXTUUMUXRUUAXQXRXSUYMITUYLITZUYK
      WLUYSXKIUYKWMUYKWHYAUYMUYLIUYLUXSWIWJWNYBYBYCYJUXTSZYNUYDYQUYGUYTYMUYCADU
      YTYLUYBBUYTBUEUYTYKUYAYHYIYGYJUXTYDWQWAWRUYTYPUYFACUYTYOUYEBDYGYIYJUXTYDV
      EWRVCWSWTXCYEYF $.
      $( [29-Mar-2007] $)

    $( An equivalence to a dominance relation for disjoint sets. $)
    brdom6disj $p |- ( A ~<_ B <-> E. f ( A. x e. B E* y
      { x , y } e. f /\ A. x e. A E. y e. B { y , x } e. f ) ) $=
      ( vv vz vw wbr cv wral wrex wa cpr wcel wceq cab vg wmo wex brdom5 cop wi
      cdom wal zfpair2 eqeq1 anbi1d df-br anbi2i syl6bbr 2rexbidv wne wb cin c0
      elab incom eqtri disjne mp3an1 vex opthpr syl breq12 biimprd syl6bi imp3a
      adantrd rexlimdvv syl5bi alrimiv immo ralimia ancoms eqcom 3bitr4g bicomi
      ex ancom a1i anbi12d rexbidva rexbidv syl5bb adantr breq2 breq1 ceqsrex2v
      bitrd ralbiia biimpri csn snex simpl ss2abi df-sn sseqtr4i ssexi abrexex2
      ax-17 eleq2 mobid ralbidv cla4ev syl2an copab preq1 preq2 eqid brab mobii
      exlimiv eleq1d ralbii rexbii df-opab cuni uniex prid1 elunii adantl prid2
      cvv mpan eqeltrri abexex eqeltri breq syl2anbr impbii bitri ) CDUGLAMZBMZ
      UAMZLZBUBZADNZYQYPYRLZBDOZACNZPZUAUCZYPYQQZEMZRZBUBZADNZYQYPQZUUHRZBDOZAC
      NZPZEUCZABCDUAFGUDUUFUUQUUEUUQUAUUAUUGIMZJMZKMZQZSZUUSUUTUEYRRZPZJDOZKCOZ
      ITZRZBUBZADNZUULUVGRZBDOZACNZUUQUUDYTUVIADYPDRZUVHYSUFZBUHYTUVIUFUVNUVOBU
      VHUUGUVASZUUSUUTYRLZPZJDOKCOZUVNYSUVFUVSIUUGABUIUURUUGSZUVDUVRKJCDUVTUVDU
      VPUVCPUVRUVTUVBUVPUVCUURUUGUVAUJUKUVQUVCUVPUUSUUTYRULZUMUNUOUTUVNUVRYSKJC
      DUVNUUTCRZUVRYSUFZUUSDRZUVNUWBUWCUVNUWBPZUVPUVQYSUWEUVPYPUUSSYQUUTSPZUVQY
      SUFUWEYPUUTUPZUVPUWFUQDCURZUSSZUVNUWBUWGUWHCDURUSDCVAHVBZDCYPUUTVCVDYPYQU
      USUUTAVEZBVEZJVEZKVEZVFVGUWFYSUVQYPUUSYQUUTYRVHVIVJVKWBVLVMVNVOUVHYSBVPVG
      VQUVMUUDUVLUUCACYPCRZUVKUUBBDUWOYQDRZPUVKUUTYPSZUUSYQSZPZUVQPZJDOZKCOZUUB
      UWOUVKUXBUQUWPUVKUULUVASZUVCPZJDOZKCOZUWOUXBUVFUXFIUULBAUIUURUULSZUVDUXDK
      JCDUXGUVBUXCUVCUURUULUVAUJUKUOUTUWOUXEUXAKCUWOUXDUWTJDUWOUWDPZUXCUWSUVCUV
      QUXHUVAUULSZUWRUWQPZUXCUWSUXHUUSYPUPZUXIUXJUQUWDUWOUXKUWIUWDUWOUXKUWJDCUU
      SYPVCVDVRUUSUUTYQYPUWMUWNUWLUWKVFVGUULUVAVSUWQUWRWCVTUVCUVQUQUXHUVQUVCUWA
      WAWDWEWFWGWHWIUVQUUSYPYRLUUBKJYPYQCDUUTYPUUSYRWJUUSYQYPYRWKWLWMWFWNWOUUPU
      VJUVMPEUVGUVEKICFUVDJIDGUVDITZUVAWPZUVAWQUXLUVBITUXMUVDUVBIUVBUVCWRWSIUVA
      WTXAXBXCXCUUHUVGSZUUKUVJUUOUVMUXNUUJUVIADUXNUUIUVHBUXNBXDUUHUVGUUGXEXFXGU
      XNUUNUVLACUXNUUMUVKBDUUHUVGUULXEWGXGWEXHXIXPUUPUUFEUUKYPYQUUTUUSQZUUHRZKJ
      XJZLZBUBZADNZYQYPUXQLZBDOZACNZUUFUUOUXSUUJADUXRUUIBUXPYPUUSQZUUHRUUIKJYPY
      QUXQUWKUWLUWQUXOUYDUUHUUTYPUUSXKXQUWRUYDUUGUUHUUSYQYPXLXQUXQXMZXNXOXRUYBU
      UNACUYAUUMBDUXPYQUUSQZUUHRUUMKJYQYPUXQUWLUWKUUTYQSUXOUYFUUHUUTYQUUSXKXQUU
      SYPSUYFUULUUHUUSYPYQXLXQUYEXNXSXRUUEUXTUYCPUAUXQUXQUURUUTUUSUEZSZUXPPZJUC
      ZKUCITYGUXPKJIXTUYJKIUUHYAZUUHEVEYBZUYIUUTUYKRZJUXPUYMUYHUUTUXORUXPUYMUUT
      UUSUWNYCUUTUXOUUHYDYHYEXPUYIJIUYKUYLUXPUUSUYKRZUYHUUSUXORUXPUYNUUTUUSUWMY
      FUUSUXOUUHYDYHYEUYIITUYHITZUYGWPUYOYGIUYGWTUYGWQYIUYIUYHIUYHUXPWRWSXBYJYJ
      YKYRUXQSZUUAUXTUUDUYCUYPYTUXSADUYPYSUXRBUYPBXDYPYQYRUXQYLXFXGUYPUUCUYBACU
      YPUUBUYABDYQYPYRUXQYLWGXGWEXHYMXPYNYO $.
      $( [5-Apr-2007] $)
  $}

  $( An image of a function under a set is dominated by the set.  Proposition
     10.34 of [TakeutiZaring] p. 92. $)
  imadomg $p |- ( A e. B -> ( Fun F -> ( F " A ) ~<_ A ) ) $=
    ( wcel wfun cima cres cdm cdom wbr wa crn df-ima cvv resfunexg dmexg funres
    wfo syl expcom funforn sylib adantr fodomg syl5eqbr wss dmres inss1 eqsstri
    sylc cin ssdom2g mpi domtr sylan2 syld ) ABDZCEZCAFZCAGZHZIJZUSAIJZURUQVBUR
    UQKZUSUTLZVAICAMVDVANDZVAVEUTRZVEVAIJVDUTNDVFCABOUTNPSURVGUQURUTEVGACQUTUAU
    BUCVAVENUTUDUJUETVBUQVCUQVBVAAIJZVCUQVAAUFVHVAACHZUKACAUGAVIUHUIVAABULUMUSV
    AAUNUOTUP $.
    $( [23-Jul-2004] $)

  $( The range of a function is dominated by its domain. $)
  fnrndomg $p |- ( A e. B -> ( F Fn A -> ran F ~<_ A ) ) $=
    ( wfn crn wfo wcel cdom wbr dffn4 fodomg syl5bi ) CADACEZCFABGMAHIACJAMBCKL
    $.
    $( [1-Sep-2004] $)

  ${
    $d A f g h x y z $.  $d B f g h x y z $.
    unidom.1 $e |- A e. _V $.
    unidom.2 $e |- B e. _V $.
    $( An upper bound for the cardinality of a union.  Theorem 10.47 of
       [TakeutiZaring] p. 98.  (Revised by Mario Carneiro, 22-Mar-2013.) $)
    unidom $p |- ( A. x e. A x ~<_ B -> U. A ~<_ ( A X. B ) ) $=
      ( vf vg vh vy vz cv wral cfv wf1 wcel wi wa fveq2 syl6 wceq cdom wbr crab
      cab wex wel cuni cxp c0 wne brdom abn0 bitr4i ralbii wf co cvv vex mapval
      cmap ovex eqeltrri ss2abi ssexi ac6c5 sylbi uniex rabex wrex eluni2 rabn0
      f1f biimpi mprg fvex f1eq1 elab cop eleq2 elrab eleq1d id eleq12d anbi12d
      weq syl5bb rcla4cv simpl adantl wb syl f1eq2 rcla4v ffvelrn expcom sylan9
      bitrd com3r jcad opelxpi fveq1d eqeq2d adantr imp31 adantrr simpr biimprd
      opth anim12d adantlll f1fveq biimpd syl2anc sylbird expimpd syl5bi fveq2d
      imp an32s eqtrd opeq12d impbid1 ex dom2d mpi exlimdv exlimiv ee10 ) AKZCU
      AUBZABLZYIFKZMZYICGKZNZGUDZOZABLZFUEZYIHKZMZAGUFZGBUCZOZABUGZLZHUEZUUEBCU
      HZUAUBZYKYPUIUJZABLYSYJUUJABYJYOGUEUUJYICGEUKYOGULUMUNABYPFDYPYICYNUOZGUD
      ZCYIUTUPUULUQCYIGEAURUSCYIUTVAVBYOUUKGYICYNVLVCVDVEVFUUCUIUJZUUGAUUEAUUEU
      UCHBDVGZUUBGBDVHVEYIUUEOZUUMUUOUUBGBVIUUMGYIBVJUUBGBVKUMVMVNYRUUGUUIPZFYR
      YICYMNZABLZUUPYQUUQABYOUUQGYMYIYLVOYICYNYMVPVQUNUURUUFUUIHUURUUFUUIUURUUF
      QZUUEUQOUUIUUNUUSIJUUEUUHIKZYTMZUUTUVAYLMZMZVRZJKZYTMZUVEUVFYLMZMZVRZUQUU
      SUUTUUEOZUVABOZUVCCOZQUVDUUHOUUSUVJUVKUVLUUFUVJUVKPUURUUFUVJUVKUUTUVAOZQZ
      UVKUUDUVNAUUTUUEUUDUUABOZYIUUAOZQZAIWEZUVNUUBUVPGUUABYNUUAYIVSVTZUVRUVOUV
      KUVPUVMUVRUUAUVABYIUUTYTRZWAUVRYIUUTUUAUVAUVRWBUVTWCWDWFWGZUVKUVMWHSWIUUR
      UUFUVJUVLPUUFUVJUURUVLUUFUVJUVNUURUVLPUWAUVKUURUVACUVBUOZUVMUVLUVKUURUVAC
      UVBNZUWBUUQUWCAUVABYIUVATZUUQYICUVBNZUWCUWDYMUVBTUUQUWEWJYIUVAYLRYICYMUVB
      VPWKYIUVACUVBWLWQWMZUVACUVBVLSUWBUVMUVLUVACUUTUVBWNWOWPSWRXRWSUVAUVCBCWTS
      UUSUVJUVEUUEOZQZUVDUVITZIJWEZWJUUSUWHQZUWIUWJUWIUVAUVFTZUVCUVHTZQUWKUWJUV
      AUVCUVFUVHUUTYTVOUUTUVBVOUVEUVGVOXHUWKUWLUWMUWJUWKUWLQZUWMUVCUVEUVBMZTZUW
      JUWLUWPUWMWJUWKUWLUWOUVHUVCUWLUVEUVBUVGUVAUVFYLRXAXBWIUWNUWCUVMUVEUVAOZQZ
      UWPUWJPUWKUWCUWLUUSUVJUWCUWGUURUUFUVJUWCUUFUVJUURUWCUUFUVJUVNUURUWCPZUWAU
      VKUWSUVMUWFXCSWRXDXEXCUUFUWHUWLUWRUURUUFUWLUWHUWRUUFUWLQZUWHUWRUWTUVJUVMU
      WGUWQUUFUVJUVMPUWLUUFUVJUVNUVMUWAUVKUVMXFSXCUUFUWGUVEUVFOZUWLUWQUUFUWGUVF
      BOZUXAQZUXAUUDUXCAUVEUUEUUDUVQAJWEZUXCUVSUXDUVOUXBUVPUXAUXDUUAUVFBYIUVEYT
      RZWAUXDYIUVEUUAUVFUXDWBUXEWCWDWFWGUXBUXAXFSUWLUWQUXAUVAUVFUVEVSXGWPXIXRXS
      XJUWCUWRQUWPUWJUVACUUTUVEUVBXKXLXMXNXOXPUWJUVAUVFUVCUVHUUTUVEYTRZUWJUVCUU
      TUVGMUVHUWJUUTUVBUVGUWJUVAUVFYLUXFXQXAUUTUVEUVGRXTYAYBYCYDYEYCYFVFYGYH $.
      $( [22-Mar-2013] $) $( [25-Mar-2006] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    $( An upper bound for the cardinality of a union.  Theorem 10.47 of
       [TakeutiZaring] p. 98. $)
    unidomg $p |- ( ( A e. V /\ B e. W /\ A. x e. A x ~<_ B ) ->
                   U. A ~<_ ( A X. B ) ) $=
      ( vz vy wcel cv cdom wbr wral cuni cxp wi wceq raleq unieq imbi12d vex
      xpeq1 breq12d breq2 ralbidv xpeq2 breq2d unidom vtocl2g 3impia ) BDHCEHAI
      ZCJKZABLZBMZBCNZJKZUJFIZJKZAGIZLZURMZURUPNZJKZOUQABLZUMBUPNZJKZOULUOOGFBC
      DEURBPZUSVCVBVEUQAURBQVFUTUMVAVDJURBRURBUPUAUBSUPCPZVCULVEUOVGUQUKABUPCUJ
      JUCUDVGVDUNUMJUPCBUEUFSAURUPGTFTUGUHUI $.
      $( [25-Mar-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    uniimadom.1 $e |- A e. _V $.
    uniimadom.2 $e |- B e. _V $.
    $( An upper bound for the cardinality of the union of an image.  Theorem
       10.48 of [TakeutiZaring] p. 99. $)
    uniimadom $p |- ( ( Fun F /\ A. x e. A ( F ` x ) ~<_ B ) ->
                 U. ( F " A ) ~<_ ( A X. B ) ) $=
      ( vy wfun cv cdom wbr wral cxp cvv wcel adantr wi wrex syl syl2anc cfv wa
      cima cuni funimaex wceq fvelima ex breq1 biimpd reximi r19.36av com23 imp
      syl6 ralrimiv unidomg mp3an2 imadomg ax-mp xpdom1 domtr ) DHZAIDUAZCJKZAB
      LZUBZDBUCZUDZVHCMZJKZVJBCMZJKZVIVLJKVGVHNOZGIZCJKZGVHLZVKVCVNVFDBEUEPVGVP
      GVHVCVFVOVHOZVPQVCVRVFVPVCVRVDVOUFZABRZVFVPQZVCVRVTAVOBDUGUHVTVEVPQZABRWA
      VSWBABVSVEVPVDVOCJUIUJUKVEVPABULSUOUMUNUPVNCNOVQVKFGVHCNNUQURTVCVMVFVCVHB
      JKZVMBNOVCWCQEBNDUSUTVHBCEFVASPVIVJVLVBT $.
      $( [25-Mar-2006] $)
  $}

  ${
    $d x z A $.  $d x y z B $.  $d y z F $.
    uniimadomf.1 $e |- ( y e. F -> A. x y e. F ) $.
    uniimadomf.2 $e |- A e. _V $.
    uniimadomf.3 $e |- B e. _V $.
    $( An upper bound for the cardinality of the union of an image.  Theorem
       10.48 of [TakeutiZaring] p. 99.  This version of ~ uniimadom uses a
       bound-variable hypothesis in place of a distinct variable condition. $)
    uniimadomf $p |- ( ( Fun F /\ A. x e. A ( F ` x ) ~<_ B ) ->
                 U. ( F " A ) ~<_ ( A X. B ) ) $=
      ( vz cv cfv cdom wbr wral wfun cima cuni cxp ax-17 wcel hbfv fveq2 breq1d
      hbbr wceq cbvral uniimadom sylan2b ) AJZEKZDLMZACNEOIJZEKZDLMZICNECPQCDRL
      MUKUNAICUKISABUMDLABULEFBJZULTASUAUOLTASUODTASUDUIULUEUJUMDLUIULEUBUCUFIC
      DEGHUGUH $.
      $( [26-Mar-2006] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d y z C $.
    iundom.1 $e |- A e. _V $.
    iundom.2 $e |- B e. _V $.
    iundom.3 $e |- C e. _V $.
    $( An upper bound for the cardinality of an indexed union. ` C ` depends on
       ` x ` and should be thought of as ` C ( x ) ` . $)
    iundom $p |- ( A. x e. A C ~<_ B -> U_ x e. A C ~<_ ( A X. B ) ) $=
      ( vy vz cdom wbr wral cv wcel wceq wa copab ciun cvv ax-mp cfv cxp breq1d
      fvopab2 mpan2 ralbiia cima cuni iuneq2i wfun eqid fnopab2 fnfun funiunfvf
      wfn hbopab1 eqtr3i uniimadomf mpan syl5eqbr sylbir ) DCJKZABLAMZVCBNZHMDO
      PZAHQZUAZCJKZABLZABDRZBCUBZJKVHVBABVDVGDCJVDDSNVGDOGAHBDSUDUEZUCUFVIVJVFB
      UGUHZVKJABVGRZVJVMABVGDVLUIVFUJZVNVMOVFBUOVOAHBDVFGVFUKULBVFUMTZAIBVFVEAH
      IUPZUNTUQVOVIVMVKJKVPAIBCVFVQEFURUSUTVA $.
      $( [26-Mar-2006] $)
  $}

