$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Cardinal number theorems using Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.
    $( The value of the cardinal number function.  Definition 10.4 of
       [TakeutiZaring] p. 85.  See ~ cardval2 for a simpler version of its
       value. $)
    cardval $p |- ( card ` A ) = |^| { x e. On | x ~~ A } $=
      ( cv cen wbr con0 wrex ccrd cfv crab cint wceq numth2 cardval3a ax-mp ) A
      CBDEZAFGBHIPAFJKLABMAABNO $.
      $( [13-Sep-2013] $) $( [21-Oct-2003] $)

    $( Any set is equinumerous to its cardinal number.  Proposition 10.5 of
       [TakeutiZaring] p. 85. $)
    cardid $p |- ( card ` A ) ~~ A $=
      ( vx ccrd cfv cen wbr con0 crab wcel cint cardval wss wne ssrab2 cvv fvex
      cv c0 eqeltrri intex mpbir onint mp2an eqeltri breq1 elrab simprbi ax-mp
      ) ACDZBQZAEFZBGHZIZUIAEFZUIULJZULBAKZULGLULRMZUOULIUKBGNUQUOOIUIUOOUPACPS
      ULTUAULUBUCUDUMUIGIUNUKUNBUIGUJUIAEUEUFUGUH $.
      $( [22-Oct-2003] $)
  $}

  ${
    $d x y $.
    $( The cardinality function is a function with domain the well-orderable
       sets.  Assuming AC, this is the universe. $)
    cardf $p |- card : _V --> On $=
      ( vy vx cvv con0 ccrd wf cv cen wbr wrex cab cardf2 vex numth2 2th abbi2i
      wcel feq2i mpbir ) CDEFAGBGZHIADJZBKZDEFBALCUBDEUABCTCQUABMATNOPRS $.
      $( [13-Sep-2013] $) $( [6-Jun-2013] $)
  $}

  $( Two sets are equinumerous iff their cardinal numbers are equal.  This
     important theorem expresses the essential concept behind "cardinality" or
     "size."  This theorem appears as Proposition 10.10 of [TakeutiZaring]
     p. 85, Theorem 7P of [Enderton] p. 197, and Theorem 9 of [Suppes] p. 242
     (among others).  The Axiom of Choice is required for its proof.

     The theory of cardinality can also be developed without AC by introducing
     "card" as a primitive notion and stating this theorem as an axiom, as is
     done with the axiom for cardinal numbers in [Suppes] p. 111.  Finally, if
     we allow the Axiom of Regularity, we can avoid AC by defining the cardinal
     number of a set as the set of all sets equinumerous to it and having least
     possible rank (see ~ karden ). $)
  carden $p |- ( ( A e. C /\ B e. D ) ->
               ( ( card ` A ) = ( card ` B ) <-> A ~~ B ) ) $=
    ( wcel wa ccrd cfv wceq cen wbr cardid ensymg mpi breq2 entr syl6bi syl5com
    wi mpan2 adantr carden2b adantl impbid ) ACEZBDEZFAGHZBGHZIZABJKZUEUIUJSUFU
    EAUGJKZUIUJUEUGAJKUKALUGACMNUIUKAUHJKZUJUGUHAJOULUHBJKUJBLAUHBPTQRUAUFUJUIS
    UEABDUBUCUD $.
    $( [20-Sep-2014] $) $( [22-Oct-2003] $)

  $( Only the empty set has cardinality zero. $)
  cardeq0 $p |- ( A e. V -> ( ( card ` A ) = (/) <-> A = (/) ) ) $=
    ( wcel ccrd cfv c0 cen wbr cvv wb 0ex carden mpan2 card0 eqeq2i en0 3bitr3g
    wceq ) ABCZADEZFDEZRZAFGHZTFRAFRSFICUBUCJKAFBILMUAFTNOAPQ $.
    $( [23-Apr-2004] $)

  ${
    unsnen.1 $e |- A e. _V $.
    unsnen.2 $e |- B e. _V $.
    $( Equinumerosity of a set with a new element added. $)
    unsnen $p |- ( -. B e. A -> ( A u. { B } ) ~~ suc ( card ` A ) ) $=
      ( wcel wn csn cun ccrd cfv csuc cen cin wceq wbr disjsn word cardon cvv
      c0 onordi orddisj ax-mp cardid ensymi fvex en2sn mp2an unen mpanl12 mpan2
      wa sylbir df-suc syl6breqr ) BAEFZABGZHZAIJZUSGZHZUSKLUPAUQMTNZURVALOZABP
      VBUSUTMTNZVCUSQVDUSARUAUSUBUCAUSLOUQUTLOZVBVDULVCUSACAUDUEBSEUSSEVEDAIUFB
      USSSUGUHAUSUQUTUIUJUKUMUSUNUO $.
      $( [7-Nov-2008] $)
  $}

  $( Two sets have the dominance relationship if their cardinalities have the
     subset relationship. $)
  carddomi $p |- ( A e. V -> ( ( card ` A ) C_ ( card ` B ) -> A ~<_ B ) ) $=
    ( wcel ccrd cfv wss cdom wbr wa cen cardid ensymg mpi con0 wi cardon ssdomg
    ax-mp endomtr syl2an domentr sylancl ex ) ACDZAEFZBEFZGZABHIZUEUHJAUGHIZUGB
    KIUIUEAUFKIZUFUGHIZUJUHUEUFAKIUKALUFACMNUFODUHULPAQUFUGORSAUFUGTUABLAUGBUBU
    CUD $.
    $( [22-Oct-2003] $)

  $( Two sets have the dominance relationship iff their cardinalities have the
     subset relationship.  Equation i of [Quine] p. 232. $)
  carddom $p |- ( ( A e. V /\ B e. W ) ->
               ( ( card ` A ) C_ ( card ` B ) <-> A ~<_ B ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr wi carddomi adantr csdm cen wo cardon con0
    wn brdom2 onelssi domnsym syl56 con2d wb ontri1 mp2an syl6ibr adantl carden
    wceq eqimss syl6bir jaod syl5bi impbid ) ACEZBDEZFZAGHZBGHZIZABJKZURVCVDLUS
    ABCMNVDABOKZABPKZQUTVCABUAUTVEVCVFUSVEVCLURUSVEVBVAEZTZVCUSVGVEVGVBVAIUSBAJ
    KVETVAVBARZUBBADMBAUCUDUEVASEVBSEVCVHUFVIBRVAVBUGUHUIUJUTVFVAVBULVCABCDUKVA
    VBUMUNUOUPUQ $.
    $( [22-Oct-2003] $)

  $( Two sets have the strict dominance relationship iff their cardinalities
     have the membership relationship.  Corollary 19.7(2) of [Eisenberg]
     p. 310. $)
  cardsdom $p |- ( ( A e. V /\ B e. W ) ->
               ( ( card ` A ) e. ( card ` B ) <-> A ~< B ) ) $=
    ( wcel wa ccrd cfv wss wne cdom wbr cen csdm carddom carden necon3abid con0
    wn cardon anbi12d wb onelpss mp2an brsdom 3bitr4g ) ACEBDEFZAGHZBGHZIZUHUIJ
    ZFZABKLZABMLZSZFUHUIEZABNLUGUJUMUKUOABCDOUGUNUHUIABCDPQUAUHREUIREUPULUBATBT
    UHUIUCUDABUEUF $.
    $( [22-Oct-2003] $)

  $( Trichotomy law for dominance and strict dominance.  This theorem is
     equivalent to the Axiom of Choice. $)
  domtri $p |- ( ( A e. V /\ B e. W ) -> ( A ~<_ B <-> -. B ~< A ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr csdm wn carddom con0 wb cardon ontri1 mp2an
    cardsdom ancoms notbid syl5bb bitr3d ) ACEZBDEZFZAGHZBGHZIZABJKBALKZMZABCDN
    UJUIUHEZMZUGULUHOEUIOEUJUNPAQBQUHUIRSUGUMUKUFUEUMUKPBADCTUAUBUCUD $.
    $( [4-Jan-2004] $)

  $( Trichotomy of equinumerosity and strict dominance.  This theorem is
     equivalent to the Axiom of Choice.  Theorem 8 of [Suppes] p. 242. $)
  entric $p |- ( ( A e. V /\ B e. W ) -> ( A ~< B \/ A ~~ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen wo wn cdom domtri biimprd brdom2 syl6ib con1d df-3or
    w3o orrd sylibr ) ACEBDEFZABGHZABIHZJZBAGHZJUCUDUFSUBUEUFUBUFUEUBUFKZABLHZU
    EUBUHUGABCDMNABOPQTUCUDUFRUA $.
    $( [1-Oct-2004] $) $( [4-Jan-2004] $)

  $( Trichotomy of dominance and strict dominance. $)
  entri2 $p |- ( ( A e. V /\ B e. W ) -> ( A ~<_ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen w3o cdom entric brdom2 orbi1i df-3or bitr4i sylibr
    wo ) ACEBDEFABGHZABIHZBAGHZJZABKHZUARZABCDLUDSTRZUARUBUCUEUAABMNSTUAOPQ $.
    $( [1-Oct-2004] $) $( [4-Jan-2004] $)

  $( Trichotomy of dominance.  This theorem is equivalent to the Axiom of
     Choice.  Part of Proposition 4.42(d) of [Mendelson] p. 275. $)
  entri3 $p |- ( ( A e. V /\ B e. W ) -> ( A ~<_ B \/ B ~<_ A ) ) $=
    ( wcel wa cdom wbr csdm wo entri2 sdomdom orim2i syl ) ACEBDEFABGHZBAIHZJOB
    AGHZJABCDKPQOBALMN $.
    $( [13-Nov-2004] $) $( [4-Jan-2004] $)

  $( A set strictly dominates iff its cardinal strictly dominates. $)
  sdomsdomcard $p |- ( A ~< B <-> A ~< ( card ` B ) ) $=
    ( csdm wbr cvv wcel cfv sdomex simprd wn c0 sdom0 fvprc breq2d mtbiri con4i
    ccrd cen cardid sdomentr ensymg mpi wa wi ax-mp ex syl5com mpan2i pm5.21nii
    fvex impbid ) ABCDZBEFZABQGZCDZULAEFUMABHIUMUOUMJZUOAKCDALUPUNKACBQMNOPUMUL
    UOUMBUNRDZULUOUMUNBRDZUQBSZUNBEUAUBULUQUOUNEFULUQUCUOUDBQUJABUNETUEUFUGUMUO
    URULUSAUNBETUHUKUI $.
    $( [30-Oct-2003] $)

  $( Cantor's theorem in terms of cardinals.  This theorem tells us that no
     matter how largei a cardinal number is, there is a still larger cardinal
     number.  Theorem 18.12 of [Monk1] p. 133. $)
  canth3 $p |- ( A e. V -> ( card ` A ) e. ( card ` ~P A ) ) $=
    ( wcel ccrd cfv cpw csdm wbr canth2g cvv wb pwexg cardsdom mpdan mpbird ) A
    BCZADEAFZDECZAQGHZABIPQJCRSKABLAQBJMNO $.
    $( [11-Jul-2004] $) $( [5-Nov-2003] $)

  ${
    $d A x $.
    infxpidm.1 $e |- A e. _V $.
    $( The cross product of an infinite set with itself is idempotent.  This
       theorem (which is an AC equivalent) provides the basis for infinite
       cardinal arithmetic.  Proposition 10.40 of [TakeutiZaring] p. 95.  This
       proof follows as a corollary of ~ infxpen .  (Revised by Mario Carneiro,
       9-Mar-2013.) $)
    infxpidm $p |- ( om ~<_ A -> ( A X. A ) ~~ A ) $=
      ( com cdom wbr ccrd cfv cxp cen wss cvv wcel wb omex carddom mp2an cardom
      sseq1i mpan entr bitr3i con0 cardon infxpen sylbi cardid ensymi fvex xpen
      mpan2 3syl ) CADEZAFGZUMHZUMIEZUNAIEZAAHZAIEZULCUMJZUOULCFGZUMJZUSCKLAKLV
      AULMNBCAKKOPUTCUMQRUAUMUBLUSUOAUCUMUDSUEUOUMAIEUPAUFZUNUMATUJUQUNIEZUPURA
      UMIEZVDVCUMABVBUGZVEAUMAUMAFUHZVFUIPUQUNATSUK $.
      $( [9-Mar-2013] $) $( [17-Sep-2004] $)
  $}

  $( A cardinal strictly dominates its members.  Equivalent to Proposition
     10.37 of [TakeutiZaring] p. 93 (use ~ cardsdom to obtain the exact
     proposition from this one). $)
  cardsdomel $p |- ( A e. On -> ( A ~< B <-> A e. ( card ` B ) ) ) $=
    ( csdm wbr ccrd cfv con0 wcel sdomsdomcard cdom wn ssdom2g wb cardon ontri1
    wss mpan domtri 3imtr3d con4d cen onelssi mpsyl eleq2i cardne sylbir brsdom
    cardidm sylanbrc impbid1 syl5bb ) ABCDABEFZCDZAGHZAULHZABIUNUMUOUNUOUMUNULA
    PZULAJDZUOKZUMKZULAGLULGHZUNUPURMBNZULAOQUTUNUQUSMVAULAGGRQSTUOAULJDZAULUAD
    KZUMUTUOAULPVBVAULAVAUBAULGLUCUOAULEFZHVCVDULABUHUDAULUEUFAULUGUIUJUK $.
    $( [2-Nov-2003] $)

  ${
    $d x A $.
    $( An alternate version of the value of the cardinal number of a set.
       Compare ~ cardval .  This theorem could be used to give us a simpler
       definition of ` card ` in place of ~ df-card .  It apparently does not
       occur in the literature. $)
    cardval2 $p |- ( card ` A ) = { x e. On | x ~< A } $=
      ( cv csdm wbr con0 crab wcel wa cab ccrd cfv df-rab cardon oneli pm4.71ri
      cardsdomel pm5.32i bitr4i abbii abid2 3eqtr2ri ) ACZBDEZAFGUCFHZUDIZAJUCB
      KLZHZAJUGUDAFMUHUFAUHUEUHIUFUHUEUGUCBNOPUEUDUHUCBQRSTAUGUAUB $.
      $( [7-Nov-2003] $)
  $}

  ${
    $d x y z A $.
    $( The collection of ordinal numbers dominated by a set is an ordinal
       number.  (In general, not all collections of ordinal numbers are
       ordinal.)  Theorem 56 of [Suppes] p. 227.  This theorem can be proved
       (with a longer proof) without the Axiom of Choice; see ~ hartogs . $)
    ondomon $p |- ( A e. V -> { x e. On | x ~<_ A } e. On ) $=
      ( vy vz wcel cv cdom wbr con0 crab word wss wa wi wal cvv imp breq1 elrab
      wtr onelon vex onelss ssdomg mpsyl domtr anim2i anassrs sylan exp31 com12
      jca imp3a 3imtr4g gen2 dftr2 mpbir ssrab2 ordon trssord mp3an cpw csdm wb
      wral elex canth2g domsdomtr sylan2 expcom ralrimivw syl ss2rab sylibr cfv
      ccrd cardval2 fvex eqeltrri ssex elong 3syl mpbiri ) BCFZAGZBHIZAJKZJFZWH
      LZWHUAZWHJMJLWJWKDGZEGZFZWMWHFZNWLWHFZOZEPDPWQDEWNWOWPWNWMJFZWMBHIZNWLJFZ
      WLBHIZNZWOWPWNWRWSXBWRWNWSXBOWRWNWSXBWRWNNZWTWLWMHIZNWSXBXCWTXDWMWLUBWLQF
      XCWLWMMZXDDUCWRWNXEWMWLUDRWLWMQUEUFUMWTXDWSXBXDWSNXAWTWLWMBUGUHUIUJUKULUN
      WGWSAWMJWFWMBHSTWGXAAWLJWFWLBHSTUORUPDEWHUQURWGAJUSUTWHJVAVBWEWHWFBVCZVDI
      ZAJKZMZWHQFWIWJVEWEWGXGOZAJVFZXIWEBQFZXKBCVGXLXJAJWGXLXGXLWGBXFVDIXGBQVHW
      FBXFVIVJVKVLVMWGXGAJVNVOWHXHXFVQVPXHQAXFVRXFVQVSVTWAWHQWBWCWD $.
      $( [7-Nov-2003] $)
  $}

  ${
    $d x y A $.  $d y V $.
    $( The smallest ordinal that strictly dominates a set is a cardinal. $)
    cardmin $p |- ( A e. V -> ( card ` |^| { x e. On | A ~< x } ) =
                      |^| { x e. On | A ~< x } ) $=
      ( vy wcel csdm wbr con0 crab cint wral ccrd cfv wceq wrex syl breq2 ax-17
      cv cvv numthcor onintrab2 sylib cdom wa wn wi onelon ex onnminsb syli vex
      wb domtri mpan sylibrd hbrab1 hbbr onminsb jctird domsdomtr syl6 ralrimiv
      hbint iscard sylanbrc ) BCEZBASZFGZAHIZJZHEZDSZVKFGZDVKKVKLMVKNVGVIAHOZVL
      ABCUAZVIAUBUCZVGVNDVKVGVMVKEZVMBUDGZBVKFGZUEVNVGVRVSVTVGVRBVMFGZUFZVSVRVG
      VMHEZWBVGVLVRWCUGVQVLVRWCVKVMUHUIPVIWAAVMVHVMBFQUJUKVMTEVGVSWBUMDULVMBTCU
      NUOUPVGVOVTVPVIVTAADBVKFVMBEARVMFEARADVJVIADHUQVDURVHVKBFQUSPUTVMBVKVAVBV
      CDVKVEVF $.
      $( [20-Sep-2014] $) $( [28-Oct-2003] $)
  $}

  ${
    $d A x $.  $d V x $.
    $( A set is finite iff its cardinal is a natural number.  (Contributed by
       Jeff Madsen 2-Sep-2009.) $)
    ficard $p |- ( A e. V -> ( A e. Fin <-> ( card ` A ) e. om ) ) $=
      ( vx wcel cfn ccrd cfv com cv cen wrex isfi wa wceq carden wi cardnn eqtr
      wbr adantl expcom eleq1a syld sylbird rexlimdva syl5bi eqcomd mpbid ancld
      syl ex breq2 rcla4ev sylibr syl6 impbid ) ABDZAEDZAFGZHDZURACIZJSZCHKZUQU
      TCALZUQVBUTCHUQVAHDZMVBUSVAFGZNZUTAVABHOVEVGUTPUQVEVGUSVANZUTVEVFVANZVGVH
      PVAQVGVIVHUSVFVARUAUJVAHUSUBUCTUDUEUFUQUTUTAUSJSZMZURUQUTVJUQUTVJUQUTMUSU
      SFGZNZVJUTVMUQUTVLUSUSQUGTAUSBHOUHUKUIVKVCURVBVJCUSHVAUSAJULUMVDUNUOUP $.
      $( [2-Sep-2009] $)
  $}

  ${
    konigth.1 $e |- A e. _V $.
    konigth.2 $e |- S = U_ i e. A ( M ` i ) $.
    konigth.3 $e |- P = X_ i e. A ( N ` i ) $.
    ${
      $d A a e f i $.  $d D a e $.  $d E a i $.  $d M a f $.  $d N a e f $.
      $d P a e f $.  $d S a e f $.
      konigth.4 $e |- D = ( i e. A |-> ( a e. ( M ` i ) |->
        ( ( f ` a ) ` i ) ) ) $.
      konigth.5 $e |- E = ( i e. A |-> ( e ` i ) ) $.
      $( Lemma for ~ konigth . $)
      konigthlem $p |- ( A. i e. A ( M ` i ) ~< ( N ` i ) -> S ~< P ) $=
        ( cfv c0 wcel cvv cv csdm wbr wral wn wfo wex crn cdif wne wa cdom fvex
        wfn cmpt rgenw eqid mptfng mpbi wceq mptex fvmpt2 mpan2 fneq1d fnrndomg
        mpbiri mpsyl domsdomtr sylan sdomdif syl ralimiaa difss ssexi ac6c5 weq
        equid cixp eldifi eleq1d syl5ibr ralimia jctil eqeltri sylibr syl6eleqr
        wi elixp wrex foelrn expcom ciun eleq2i eliun bitri hbra1 hban ad2antrl
        ax-17 w3a fveq1d sylan9eq eqcomd eqtr3d fnfvelrn adantl eqeltrd 3adant1
        fveq1 simp1 simp3l ra4 eldifn syl6 pm2.65i pm2.21i 3expia exp3a rexlimd
        sylc syl5bi com23 rexlimdv syl9r mpd mt2i exlimiv 3syl nexdv 0dom 0sdom
        ex mpan sylib ralimi neeq1i ixpexg mp2an ac9 3bitr4i iunex domtri mtand
        wb biimpri fodomr mp3an1 syl2an notnot ) GUAZIQZUUJJQZUBUCZGAUDZDCUBUCZ
        UEZUEUUOUUNUUPDCFUAZUFZFUGZUUNUURFUUNUULUUJBQZUHZUIZRUJZGAUDUUJEUAZQZUV
        BSZGAUDZEUGUURUEZUUMUVCGAUUJASZUUMUKUVAUULUBUCZUVCUVIUVAUUKULUCZUUMUVJU
        UKTSUVIUUTUUKUNZUVKUUJIUMZUVIUVLKUUKUUJKUAZUUQQZQZUOZUUKUNZUVPTSZKUUKUD
        UVRUVSKUUKUUJUVOUMZUPKUUKUVPUVQUVQUQZURUSUVIUUKUUTUVQUVIUVQTSUUTUVQUTKU
        UKUVPUVMVAGAUVQTBOVBVCZVDVFZUUKTUUTVEVGUVAUUKUULVHVIUVAUULVJVKVLGAUVBEL
        UVBUULUUJJUMZUULUVAVMVNVOUVGUVHEUVGUURFFVPZFVQUVGHCSZUURUWEUEZWGUVGHGAU
        ULVRZCUVGHAUNZUUJHQZUULSZGAUDZUKHUWHSUVGUWLUWIUVFUWKGAUVFUWKUVIUVEUULSU
        VEUULUVAVSUVIUWJUVEUULUVIUVETSZUWJUVEUTZUUJUVDUMZGAUVETHPVBVCZVTWAWBUWM
        GAUDUWIUWMGAUWOUPGAUVEHPURUSWCGAUULHHGAUVEUOTPGAUVELVAWDWHWENWFUWFUURHU
        VOUTZKDWIZUVGUWGUURUWFUWRKDCHUUQWJWKUVGUWQUWGKDUVGUWQUVNDSZUWGUVGUWQUWS
        UWGWGUWSUVNUUKSZGAWIZUVGUWQUKZUWGUWSUVNGAUUKWLZSUXADUXCUVNMWMGUVNAUUKWN
        WOUXBUWTUWGGAUVGUWQGUVFGAWPUWQGWSWQUWGGWSUXBUVIUWTUWGUVGUWQUVIUWTUKZUWG
        UVGUWQUXDWTZUWGUXEUVEUVASZUWQUXDUXFUVGUWQUXDUKZUVEUVNUUTQZUVAUXGUWJUVEU
        XHUVIUWNUWQUWTUWPWRUWQUXDUWJUVPUXHUUJHUVOXIUXDUXHUVPUVIUWTUXHUVNUVQQZUV
        PUVIUVNUUTUVQUWBXAUWTUVSUXIUVPUTUVTKUUKUVPTUVQUWAVBVCXBXCXBXDUXDUXHUVAS
        ZUWQUVIUVLUWTUXJUWCUUKUVNUUTXEVIXFXGXHUXEUVGUVIUXFUEZUVGUWQUXDXJUVGUWQU
        VIUWTXKUVGUVIUVFUXKUVFGAXLUVEUULUVAXMXNXTXOXPXQXRXSYAYLYBYCYDYEYFYGYHYI
        UUNRCUBUCZCDULUCZUUSUUPUUNUULRUJZGAUDZUXLUUMUXNGAUUMRUULUBUCZUXNRUUKULU
        CUUMUXPUUKYJRUUKUULVHYMUULUWDYKYNYOCRUJUWHRUJUXLUXOCUWHRNYPCCUWHTNATSUU
        LTSZGAUDUWHTSLUXQGAUWDUPGAUULTTYQYRWDZYKGAUULLUWDYSYTWEUXMUUPCTSDTSZUXM
        UUPUUDUXRDUXCTMGAUUKLUVMUUAWDZCDTTUUBYRUUEUXSUXLUXMUUSUXTDCTFUUFUUGUUHU
        UCUUOUUIWE $.
        $( [22-Feb-2013] $)
    $}
    $d A a e f i $.  $d A a e i j $.  $d M a b e f $.  $d N a e f $.
    $d P a e f $.  $d S a e f $.  $d b e f i $.
    $( Konig's Theorem.  If ` m ( i ) ~< n ( i ) ` for all ` i e. A ` , then
       ` sum_ i e. A m ( i ) ~< prod_ i e. A n ( i ) ` , where the sums and
       products stand in for disjoint union and infinite cartesian product.
       The version here is proven with regular unions rather than disjoint
       unions for convenience, but the version with disjoint unions is clearly
       a special case of this version.  The Axiom of Choice is needed for this
       proof, but it contains AC as a simple corollary (letting
       ` m ( i ) = (/) ` , this theorem says that an infinite cartesian product
       of nonempty sets is nonempty), so this is an AC equivalent.  Theorem
       11.26 of [TakeutiZaring] p. 107.  (Contributed by Mario Carneiro,
       22-Feb-2013.) $)
    konigth $p |- ( A. i e. A ( M ` i ) ~< ( N ` i ) -> S ~< P ) $=
      ( vb vf ve vj va cv cfv cmpt weq fveq2 cbvmptv fveq1d mpteq2i konigthlem
      ) ADAJDOZEPZUDJOZKOZPZPZQZQBCLKDMAMOZLOZPZQEFNGHIDAUJNUEUDNOZUGPZPZQJNUEU
      IUPJNRUDUHUOUFUNUGSUATUBMDAUMUDULPUKUDULSTUC $.
      $( [22-Feb-2013] $)
  $}

  $( The power set of an aleph dominates the successor aleph.  (The Generalized
     Continuum Hypothesis says they are equinumerous.) $)
  alephsucpw $p |- ( aleph ` suc A ) ~<_ ~P ( aleph ` A ) $=
    ( csuc cale cfv cpw cdom wbr csdm wn alephsucpw2 cvv wcel fvex domtri mp2an
    wb pwex mpbir ) ABZCDZACDZEZFGZUBTHGIZAJTKLUBKLUCUDPSCMUAACMQTUBKKNOR $.
    $( [27-Aug-2005] $)

  $( The set exponentiation of 2 to the aleph-zero has cardinality of at least
     aleph-one.  (If we were to assume the Continuum Hypothesis, their
     cardinalities would be the same.) $)
  aleph1 $p |- ( aleph ` 1o ) ~<_ ( 2o ^m ( aleph ` (/) ) ) $=
    ( c1o cale cfv c0 csuc c2o cmap co cdom df-1o fveq2i cpw wbr alephsucpw cvv
    wcel cen wb ovex fvex pw2en domen2 mp2an mpbi eqbrtri ) ABCDEZBCZFDBCZGHZIA
    UFBJKUGUHLZIMZUGUIIMZDNUIOPUJUIQMUKULRFUHGSUHDBTUAUJUIUGOUBUCUDUE $.
    $( [7-Jul-2004] $)

  ${
    $d x y z w A $.
    $( An alternate way to express the value of the aleph function for nonzero
       arguments.  Theorem 64 of [Suppes] p. 229. $)
    alephval2 $p |- ( ( A e. On /\ (/) e. A ) -> ( aleph ` A ) =
                    |^| { x e. On | A. y e. A ( aleph ` y ) ~< x } ) $=
      ( vz con0 wcel wa cale cfv csdm wbr wral wceq wb ccrd com cdom cvv mp2an
      wi c0 cv crab wn cint alephordi ralrimiv alephon jctil breq2 elrab sylibr
      ralbidv adantr oneli cardsdomel alephcard eleq2i syl6rbb syl ibi omex vex
      wo entri3 wss carddom cardom sseq1i bitr3i wrex cardidm cardalephex mpbii
      alephord ancoms cen cardid sdomen1 breq1 syl5rbbr sylan9bb breq1d sdomirr
      fveq2 rcla4v sdomen2 syl5bbr mtbiri nsyli com12 adantl sylbird exp31 syl5
      rexlimdv syl5bi wne ne0i onelon alephgeom ssdomg ax-mp sylbi domtr sylan2
      domnsym expr r19.2z ex syl2im rexnal syl6ib expimpd a1d com3r mpi simprbi
      jaod con3i syl56 ssrab2 oneqmini syl2anc ) CEFZUACFZGZCHIZBUBZHIZAUBZJKZB
      CLZAEUCZFZDUBZYNFZUDZDYHLZYHYNUEMZYEYOYFYEYHEFZYJYHJKZBCLZGYOYEUUCUUAYEUU
      BBCYICUFUGCUHZUIYMUUCAYHEYKYHMYLUUBBCYKYHYJJUJUMUKULUNYGYRDYHYPYHFZYPYHJK
      ZYGYJYPJKZBCLZUDZYRUUEUUFUUEYPEFZUUEUUFNYHYPUUDUOUUJUUFYPYHOIZFUUEYPYHUPU
      UKYHYPCUQURUSUTVAYGPYPQKZYPPQKZVDZUUFUUITZPRFZYPRFZUUNVBDVCZPYPRRVESYGUUL
      UUOUUMYEUULUUOTYFUULPYPOIZVFZYEUUOUULPOIZUUSVFZUUTUUPUUQUVBUULNVBUURPYPRR
      VGSUVAPUUSVHVIVJUUTUUSYKHIZMZAEVKZYEUUOUUTUUSOIUUSMUVEYPVLAUUSVMVNYEUVDUU
      OAEYEYKEFZUVDUUOYEUVFGZUVDGUUFYKCFZUUIUVGUVHUVCYHJKZUVDUUFUVFYEUVHUVINYKC
      VOVPUUFUUSYHJKZUVDUVIUUQUUSYPVQKZUVJUUFNUURYPVRZUUSYPYHRVSSUUSUVCYHJVTWAW
      BUVDUVHUUITUVGUVHUVDUUIUVHUUHUVCYPJKZUVDUUGUVMBYKCYIYKMYJUVCYPJYIYKHWEWCW
      FUVDUVMUVCUVCJKZUVCWDUVMUVCUUSJKZUVDUVNUUQUVKUVOUVMNUURUVLUUSYPUVCRWGSUUS
      UVCUVCJUJWHWIWJWKWLWMWNWPWOWQUNUUMUUFYGUUIUUMYGUUITUUFUUMYEYFUUIYFUUMYEGZ
      UUIYFUVPUUGUDZBCVKZUUIYFCUAWRZUVPUVQBCLZUVRCUAWSUVPUVQBCUUMYEYICFZUVQYEUW
      AGUUMYIEFZUVQCYIWTUUMUWBGYPYJQKZUVQUWBUUMPYJQKZUWCUWBPYJVFZUWDYIXAUUPUWEU
      WDTVBPYJRXBXCXDYPPYJXEXFYPYJXGUTXFXHUGUVSUVTUVRUVQBCXIXJXKUUGBCXLXMWKXNXO
      XPXSXQYQUUHYQUUJUUHYMUUHAYPEYKYPMYLUUGBCYKYPYJJUJUMUKXRXTYAUGYNEVFYOYSGYT
      TYMAEYBDYHYNYCXCYD $.
      $( [15-Nov-2003] $)
  $}

  ${
    $d x y z w A $.
    dominfac.1 $e |- A e. _V $.
    $( A nonempty set that is a subset of its union is infinite.  This version
       is proved from ~ ax-ac .  See ~ dominf for a version proved from
       ~ ax-cc . $)
    dominfac $p |- ( ( A =/= (/) /\ A C_ U. A ) -> om ~<_ A ) $=
      ( vx vz vw vy cv c0 wne cuni wss wa com cdom wbr wi wceq eqid cvv wcel id
      neeq1 unieq sseq12d anbi12d breq2 imbi12d cpw cin crab crdg cres inf3lem6
      copab wf1 vex pwex f1dom2g ax-mp csdm wn cfn pwfi isfinite3 3imtr3i con3i
      biimpi wb omex domtri mp2an 3imtr4i 3syl vtocl ) CGZHIZVOVOJZKZLZMVONOZPA
      HIZAAJZKZLZMANOZPCABVOAQZVSWDVTWEWFVPWAVRWCVOAHUBWFVOAVQWBWFUAVOAUCUDUEVO
      AMNUFUGVSMVOUHZDGEGVOUIFGKEVOUJQFDUNZHUKMULZUOZMWGNOZVTCFDEAAWIWHWHRWIRBB
      UMWGSTZWJWKPVOCUPZUQZMWGSWIURUSWGMUTOZVAZVOMUTOZVAZWKVTWQWOVOVBTZWGVBTZWQ
      WOWSWTVOVCVGVOVDWGVDVEVFMSTZWLWKWPVHVIWNMWGSSVJVKXAVOSTVTWRVHVIWMMVOSSVJV
      KVLVMVN $.
      $( [25-Mar-2007] $)
  $}


