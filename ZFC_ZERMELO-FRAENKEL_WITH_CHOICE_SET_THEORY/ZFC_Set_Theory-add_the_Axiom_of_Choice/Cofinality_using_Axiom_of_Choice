$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Cofinality using Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A f x y $.  $d A f y z $.
    $( A successor aleph is regular.  Theorem 11.15 of [TakeutiZaring] p. 103.
       (Contributed by Mario Carneiro, 9-Mar-2013.) $)
    alephreg $p |- ( cf ` ( aleph ` suc A ) ) = ( aleph ` suc A ) $=
      ( vf vx vy vz con0 wcel cale cfv ccf wceq csdm wbr wa cdom cv wss syl2anc
      wral syl csuc wn alephordilem1 cxp wf1 wrex wex alephon cff1 ax-mp wf f1f
      ciun ad2antrr simplr oneli ffvelrn onelon sylancr onsssuc sylan2 rexbidva
      wb anassrs eliun syl6bbr ancoms ralbidva dfss3 biimpa ssdomg mpsyl simprl
      suceloni wlim alephislim limsuc sylbi breq1 ccrd alephcard iscard simprbi
      vtoclri alephsucdom syl5ibr sylbid syl5 expdimp ralrimiv fvex sucex domtr
      iundom expcom exlimdv mpi cen alephgeom infxpen mpan syl6 domentr sylsyld
      com xpdom1 imp domnsym ex mt2d cfon cfle onsseleq mpbii mp2an ori cf0 cdm
      wo wfn alephfnon fndm eleq2i sucelon bitr4i ndmfv sylnbir 3eqtr4a pm2.61i
      c0 fveq2d ) AFGZAUAZHIZJIZYNKZYLYOYNGZUBYPYLYQAHIZYNLMZAUCYLYQYSUBZYLYQNZ
      YNYROMZYTUUAYNYOYRUDZOMZUUCYROMZUUBUUAYOYNBPZUEZCPZDPZUUFIZQZDYOUFZCYNSZN
      ZBUGZUUDYNFGZUUOYMUHZCDYNBUIUJUUAUUNUUDBUUNUUAUUDUUNUUANZYNDYOUUJUAZUMZOM
      ZUUTUUCOMZUUDUUPUURYNUUTQZUVAUUQUURYOYNUUFUKZUUMUVCUUGUVDUUMUUAYOYNUUFULU
      NZUUGUUMUUAUOUVDUUMUVCUVDUUMUUHUUTGZCYNSUVCUVDUULUVFCYNUUHYNGUVDUUHFGZUUL
      UVFVCZYNUUHUUQUPUVGUVDUVHUVGUVDNZUULUUHUUSGZDYOUFUVFUVIUUKUVJDYOUVGUVDUUI
      YOGZUUKUVJVCZUVDUVKNZUVGUUJFGZUVLUVMUUPUUJYNGZUVNUUQYOYNUUIUUFUQZYNUUJURU
      SUUHUUJUTVAVDVBDUUHYOUUSVEVFVGVAVHCYNUUTVIVFVJRYNUUTFVKVLUURYLUVDUVBUUNYL
      YQVMUVEYLUVDNZUUSYROMZDYOSUVBUVQUVRDYOYLUVDUVKUVRUVMUVOYLUVRUVPYLUVOUUSYN
      GZUVRYLYMFGZUVOUVSVCZAVNUVTYNVOUWAYMVPYNUUJVQVRTUVSUVRYLUUSYNLMZEPZYNLMZU
      WBEUUSYNUWCUUSYNLVSYNVTIYNKZUWDEYNSZYMWAUWEUUPUWFEYNWBWCUJZWDUUSAWEWFWGWH
      WIWJDYOYRUUSYNJWKAHWKZUUJUUIUUFWKWLWNTRYNUUTUUCWMRWOWPWQYLYQUUEYLYRYRUDZY
      RWRMZYQUUCUWIOMZUUEYLXEYRQZUWJAWSYRFGUWLUWJAUHYRWTXAVRYLYQYOYROMZUWKYQUWM
      YLYOYNLMZUWDUWNEYOYNUWCYOYNLVSUWGWDYOAWEWFYOYRYRUWHUWHXFXBUWKUWJUUEUUCUWI
      YRXCWOXDXGYNUUCYRWMRYNYRXHTXIXJYQYPYOFGZUUPYQYPXSZYNXKUUQUWOUUPNYOYNQUWPY
      NXLYOYNXMXNXOXPTYLUBZYJJIYJYOYNXQUWQYNYJJYLYMHXRZGZYNYJKUWSUVTYLUWRFYMHFX
      TUWRFKYAFHYBUJYCAYDYEYMHYFYGZYKUWTYHYI $.
      $( [9-Mar-2013] $)
  $}

  ${
    $d A f w y z $.  $d A f x y z $.  $d H x $.  $d f s x y $.
    pwcfsdom.1 $e |- H = ( y e. ( cf ` ( aleph ` A ) ) |->
       { s e. On | s ~<_ ( f ` y ) } ) $.
    $( A corollary of Konig's Theorem ~ konigth .  Theorem 11.28 of
       [TakeutiZaring] p. 108.  (Contributed by Mario Carneiro,
       20-Mar-2013.) $)
    pwcfsdom $p |- ( aleph ` A ) ~<
       ( ( aleph ` A ) ^m ( cf ` ( aleph ` A ) ) ) $=
      ( vx con0 wcel cale cfv ccf csdm wbr c0 wceq cvv wa com c2o vz vw cmap co
      csuc wrex wlim w3o onzsl biimpi cfom aleph0 fveq2i 3eqtr4i fveq2d 3eqtr4a
      cv fveq2 cdom cpw cen fvex canth2 pw2en wi sdomentr ax-mp mp2an alephgeom
      ovex wss omelon 2onn onelss sstr mpan sylbi ssdomg mapdom1 3syl sdomdomtr
      mp2 sylancr oveq2 breq2d syl5ibrcom syl5 alephreg rexlimivw wsmo wral w3a
      wf wex alephon cfsmo cixp limelon ciun crn cuni cab wfn ffn fnrnfv unieqd
      dfiun2 syl6eqr ad2antrl fnfvelrn sylan sseq2 rcla4ev ex rexlimdva ralimdv
      syl imp adantl wb omsublim syl2an mpbird crab ffvelrn ccrd iscard simprbi
      frn mp2b breq1 elrab mpan2 mpsyl ralrimiva eqid wn cmpt c1o syl6eq adantr
      coflim ondomcard domrefg biimpri rcla4cv hartogs rabbidv konigth eqbrtrrd
      eqtr3d oneli fvmptg alephlim eleq2d eliun alephcard vtoclri domnsym con2i
      ontri1 sylibr alephord2i ontr2 ancoms exp31 syl5bi sylbid sylan9r cbvmptv
      rexlimdv ss2ixp ixpconst syl6sseq ssdom2g adantrr syl2anc 3adant2 exlimiv
      eqtri fmptd expcom a1i 3jaod mpd cdm alephfnon eleq2i ndmfv wne 1n0 elexi
      fndm 1on 0sdom mpbir cf0 oveq12d 0ex map0e breq12d mpbiri sylnbir pm2.61i
      id ) BHIZBJKZUXGUXGLKZUCUDZMNZUXFBOPZBGUQZUEZPZGHUFZBQIBUGRZUHZUXJUXFUXQG
      BUIUJUXFUXKUXJUXOUXPUXKUXHUXGPZUXFUXJUXKOJKZLKZUXSUXHUXGSLKSUXTUXSUKUXSSL
      ULUMULUNUXKUXGUXSLBOJURZUOUYAUPUXFUXJUXRUXGUXGUXGUCUDZMNZUXFUXGTUXGUCUDZM
      NZUYDUYBUSNZUYCUXGUXGUTZMNZUYGUYDVANZUYEUXGBJVBZVCUXGUYJVDUYDQIUYHUYIRUYE
      VETUXGUCVJUXGUYGUYDQVFVGVHUXFTUXGVKZTUXGUSNZUYFUXFSUXGVKZUYKBVITSVKZUYMUY
      KSHITSIZUYNVLVMSTVNWBTSUXGVOVPVQUYOUYKUYLVEVMTUXGSVRVGTUXGUXGUYJUYJVSVTUY
      BQIUYEUYFRUYCVEUXGUXGUCVJUXGUYDUYBQWAVGWCUXRUXIUYBUXGMUXHUXGUXGUCWDWEWFZW
      GUXOUXRUXFUXJUXNUXRGHUXNUXMJKZLKUYQUXHUXGUXLWHUXNUXGUYQLBUXMJURZUOUYRUPWI
      UYPWGUXPUXJVEZUXFUXGHIZUXHUXGCUQZWMZVUAWJZUAUQZUBUQZVUAKZVKZUBUXHUFZUAUXG
      WKZWLZCWNUYSBWOZUAUBUXGCWPVUJUYSCVUBVUIUYSVUCUXPVUBVUIRZUXJUXPVULRUXGGUXH
      UXLDKZWQZMNZVUNUXIUSNZUXJUXPUXFVULVUOBQWRZUXFVULRZGUXHUXLVUAKZWSZUXGVUNMV
      URVUAWTZXAZVUTUXGVUBVVBVUTPUXFVUIVUBVVBAUQZVUSPGUXHUFAXBZXAZVUTVUBVUAUXHX
      CZVVBVVEPUXHUXGVUAXDZVVFVVAVVDGAUXHVUAXEXFXQGAUXHVUSUXLVUAVBZXGXHXIVURVVB
      UXGPZVUDVVCVKZAVVAUFZUAUXGWKZVULVVLUXFVUBVUIVVLVUBVUHVVKUAUXGVUBVUGVVKUBU
      XHVUBVUEUXHIZRZVUGVVKVVNVUFVVAIZVUGVVKVUBVVFVVMVVOVVGUXHVUEVUAXJXKVVJVUGA
      VUFVVAVVCVUFVUDXLXMXKXNXOXPXRXSUXFUXGUGVVAUXGVKZVVIVVLXTVULBYAVUBVVPVUIUX
      HUXGVUAYIUUAUAAUXGVVAUUBYBYCUUKVUBVUTVUNMNZUXFVUIVUBVUSVUMMNZGUXHWKVVQVUB
      VVRGUXHVUBUXLUXHIZRZVVRVUSEUQZVUSUSNZEHYDZMNZVVTVUSUXGIZVUSHIZVWDUXHUXGUX
      LVUAYEZUXGVUSVUKUULVVCVWCMNZAVWCWKZVWFVUSVWCIZVWDVUSQIZVWCYFKVWCPZVWIVVHE
      VUSQUUCVWLVWCHIZVWIAVWCYGYHYJVWFVUSVUSUSNZVWJVWKVWNVVHVUSQUUDVGVWJVWFVWNR
      VWBVWNEVUSHVWAVUSVUSUSYKYLUUEYMVWHVWDAVUSVWCVVCVUSVWCMYKUUFYNVTVVSVVRVWDX
      TVUBVVSVUMVWCVUSMVVSVWMVUMVWCPVWKVWMVVHEVUSQUUGVGZAUXLVWAVVCVUAKZUSNZEHYD
      ZVWCUXHHDVVCUXLPZVWQVWBEHVWSVWPVUSVWAUSVVCUXLVUAURWEUUHZFUUMYMWEXSYCYOUXH
      VUNVUTGVUADUXGLVBZVUTYPVUNYPUUIXQXIUUJXKUXPVUBVUPVUIUXIQIZUXPVUBRZVUNUXIV
      KZVUPUXGUXHUCVJZVXCUXHUXGDWMZVUMUXGVKZGUXHWKZVXDVXCGUXHVWCUXGDVXCVVSVWCUX
      GIZVUBVVSVWEUXPVXIVUBVVSVWEVWGXNUXPVWEVUSABVVCJKZWSZIZVXIUXPUXGVXKVUSABQU
      UNUUOUXPUXFVXLVXIVEVUQVXLVUSVXJIZABUFUXFVXIAVUSBVXJUUPUXFVXMVXIABUXFVVCBI
      ZVXMVXIVXMUXFVXNRZVXIVXMVWCVXJVKZVXJUXGIZVXIVXOVXMVUSVXJMNZVXPVUDVXJMNZVX
      RUAVUSVXJVUDVUSVXJMYKVXJYFKVXJPZVXSUAVXJWKZVVCUUQVXTVXJHIZVYAUAVXJYGYHVGU
      URVXRVXJVWCIZYQZVXPVYCVXRVYCVXJVUSUSNZVXRYQVYCVYBVYEVWBVYEEVXJHVWAVXJVUSU
      SYKYLYHVXJVUSUUSXQUUTVWMVYBVXPVYDXTVWOVVCWOVWCVXJUVAVHUVBXQUXFVXNVXQVVCBU
      VCXRVWMUYTVXPVXQRVXIVEVWOVUKVWCVXJUXGUVDVHYBUVEUVFUVKUVGXQUVHUVIXRDAUXHVW
      RYRGUXHVWCYRFAGUXHVWRVWCVWTUVJUVTUWAVXFVXGGUXHUYTVXFVVSRVUMUXGIVXGVUKUXHU
      XGUXLDYEUXGVUMVNYNYOVXHVUNGUXHUXGWQUXIGUXHVUMUXGUVLGUXHUXGVXAUYJUVMUVNVTV
      UNUXIQUVOYNUVPVXBVUOVUPRUXJVEVXEUXGVUNUXIQWAVGUVQUWBUVRUVSYJUWCUWDUWEUXFB
      JUWFZIZUXJVYFHBJHXCVYFHPUWGHJUWMVGUWHVYGYQUXGOPZUXJBJUWIVYHUXJOYSMNZVYIYS
      OUWJUWKYSYSHUWNUWLUWOUWPVYHUXGOUXIYSMVYHUXEZVYHUXIOOUCUDYSVYHUXGOUXHOUCVY
      JVYHUXHOLKOUXGOLURUWQYTUWROUWSUWTYTUXAUXBXQUXCUXD $.
      $( [20-Mar-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d f x y $.  $d f y z $.
    cfpwsdom.1 $e |- B e. _V $.
    $( A corollary of Konig's Theorem ~ konigth .  Theorem 11.29 of
       [TakeutiZaring] p. 108.  (Contributed by Mario Carneiro,
       20-Mar-2013.) $)
    cfpwsdom $p |- ( 2o ~<_ B -> ( aleph ` A ) ~< ( cf `
      ( card ` ( B ^m ( aleph ` A ) ) ) ) ) $=
      ( con0 wcel cdom wbr cale cfv cmap co ccf csdm wa wceq com cvv ax-mp c1o
      c0 vx vy vf vz c2o ccrd wi cxp cen ovex cardid ensymi cv wrex crn wn fvex
      canth2 pw2en sdomentr mp2an mapdom1 sdomdomtr sylancr wb ficard isfinite3
      cpw cfn sdomdom sylbi sylbir wss alephgeom omex ssdomg syl2an domnsym syl
      domtr expcom con2d cardidm cun wo iscard3 elun df-or mpbi syl56 alephfnon
      3bitri fvelrnb syl6ib crab cmpt eqid pwcfsdom fveq2 oveq12d breq12d mpbii
      wfn rexlimivw syl6 imp ensdomtr enref mapen mapxpen entri sylancl alephon
      id xpdom2 biimpi infxpen domentr csuc nsuceq0 nemtbir df-2o breq1i syl5bb
      dom0 breq2 biimpcd adantld mtoi xpex mapdom2 expl com12 mt2d domtri nsyl2
      biimpri 1onn syl6eq fveq2d ex cdm fndm eleq2i ndmfv sylnbir wne 1n0 elexi
      0sdom mpbir oveq2 map0e cardnn df-1o fveq2i 0elon cfsuc eqtri a1d pm2.61i
      mpbiri ) ADEZUEBFGZAHIZBUVEJKZUFIZLIZMGZUGZUVCUVDUVIUVCUVDNZUVHUVEFGZUVIU
      VKUVLUVFBUVEUVHUHZJKZMGZUVKUVFUVGUVHJKZMGZUVPUVNUIGZUVOUVKUVFUVGUIGUVGUVP
      MGZUVQUVGUVFBUVEJUJZUVFUKZULUVCUVDUVSUVCUVDUAUMZHIZUVGOZUADUNZUVSUVCUVDUV
      GHUOZEZUWEUVDUVEUVFMGZUVCUVGPEZUPZUWGUVDUVEUEUVEJKZMGZUWKUVFFGZUWHUVEUVEV
      HZMGZUWNUWKUIGZUWLUVEAHUQZURUVEUWQUSUWKQEUWOUWPNUWLUGUEUVEJUJUVEUWNUWKQUT
      RVAUEBUVECUWQVBUVFQEZUWLUWMNUWHUGUVTUVEUWKUVFQVCRVDUVCUWIUWHUWIUVCUWHUPZU
      WIUVCNUVFUVEFGZUWSUWIUVFPFGZPUVEFGZUWTUVCUWIUVFVIEZUXAUWRUXCUWIVEUVTUVFQV
      FRUXCUVFPMGUXAUVFVGUVFPVJVKVLUVCPUVEVMZUXBAVNZPQEUXDUXBUGVOPUVEQVPRVKUVFP
      UVEVTVQUVFUVEVRVSWAWBUVGUFIUVGOZUWJUWGUGZUVFWCUXFUVGPUWFWDEUWIUWGWEUXGUVG
      WFUVGPUWFWGUWIUWGWHWLWIWJHDXCZUWGUWEVEWKUADUVGHWMRWNUWDUVSUADUWDUWCUWCUWC
      LIZJKZMGUVSUBUWBUCUBUXIUDUMUBUMUCUMIFGUDDWOWPZUDUXKWQWRUWDUWCUVGUXJUVPMUW
      DXNZUWDUWCUVGUXIUVHJUXLUWCUVGLWSWTXAXBXDXEXFUVFUVGUVPXGVDUVPUVFUVHJKZUVNU
      VGUVFUIGUVHUVHUIGUVPUXMUIGUWAUVHUVGLUQZXHUVGUVFUVHUVHUVTUXNXIVABUVEUVHCUW
      QUXNXJXKUVNQEUVQUVRNUVOUGBUVMJUJUVFUVPUVNQUTRXLUVLUVKUVOUPZUVLUVCUVDUXOUV
      LUVCNZUVDNUVNUVFFGZUXOUXPUVMUVEFGZUVMTOZBTOZNZUPUXQUVDUVLUVMUVEUVEUHZFGUY
      BUVEUIGZUXRUVCUVHUVEUVEUWQUWQXOUVCUVEDEUXDUYCAXMUVCUXDUXEXPUVEXQVDUVMUYBU
      VEXRVQUVDUYASXSZTFGZUYEUYDTSXTUYDYEYAUVDUXTUYEUXSUXTUVDUYEUVDUYDBFGUXTUYE
      UEUYDBFYBYCBTUYDFYFYDYGYHYIUVMUVEBUVEUVHUWQUXNYJUWQCYKVQUVNUVFVRVSYLYMYNU
      VLUVIUPZUVHQEUVEQEUVLUYFVEUXNUWQUVHUVEQQYOVAYQYPUUAUVCUPUVETOZUVJUVCAHUUB
      ZEUYGUYHDAUXHUYHDOWKDHUUCRUUDAHUUEUUFUYGUVIUVDUYGUVITSMGZUYISTUUGUUHSSPYR
      UUIUUJUUKUYGUVETUVHSMUYGXNUYGUVHSLIZSUYGUVGSLUYGUVGSUFIZSUYGUVFSUFUYGUVFB
      TJKSUVETBJUULBCUUMYSYTSPEUYKSOYRSUUNRYSYTUYJTXSZLIZSSUYLLUUOUUPTDEUYMSOUU
      QTUURRUUSYSXAUVBUUTVSUVA $.
      $( [20-Mar-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d f x y $.  $d f y z $.
    $( From ~ canth2 , we know that ` ( aleph `` 0 ) < ( 2 ^ om ) ` , but we
       cannot prove that ` ( 2 ^ om ) = ( aleph `` 1 ) ` (this is the Continuum
       Hypothesis), nor can we prove that it is less than any bound whatsoever
       (i.e. the statement ` ( aleph `` A ) < ( 2 ^ om ) ` is consistent for
       any ordinal ` A ` ).  However, we can prove that ` ( 2 ^ om ) ` is not
       equal to ` ( aleph `` om ) ` , nor ` ( aleph `` ( aleph `` om ) ) ` , on
       cofinality grounds, because by Konig's Theorem ~ konigth (in the form of
       ~ cfpwsdom ), ` ( 2 ^ om ) ` has uncountable cofinality, which
       eliminates limit alephs like ` ( aleph `` om ) ` .  (The first limit
       aleph that is _not_ eliminated is ` ( aleph `` ( aleph `` 1 ) ) ` ,
       which has cofinality ` ( aleph `` 1 ) ` .)  (Contributed by Mario
       Carneiro, 21-Mar-2013.) $)
    alephom $p |- ( card ` ( 2o ^m om ) ) =/= ( aleph ` om ) $=
      ( com csdm wbr wn c2o cmap co ccrd cfv cale wne sdomirr wceq ccf cvv wcel
      c0 cdom aleph0 ax-mp 2onn elexi domrefg cfpwsdom a1i oveq2i fveq2i eqeq1i
      mp2b biimpri fveq2d wlim limom alephsing cfom eqtri syl6eq mpbii necon3bi
      breq12d ) AABCZDEAFGZHIZAJIZKALVAVCVDVCVDMZQJIZEVFFGZHIZNIZBCZVAEOPEERCVJ
      EAUAUBZEOUCQEVKUDUIVEVFAVIABVFAMVESUEVEVIVDNIZAVEVHVDNVHVDMVEVHVCVDVGVBHV
      FAEFSUFUGUHUJUKVLANIZAAULVLVMMUMAUNTUOUPUQUTURUST $.
      $( [21-Mar-2013] $)
  $}

  ${
    $d x y $.
    $( The beth function is strictly monotone.  This proof needs AC to ensure
       that each level of the hierarchy is well-orderable, and conversely this
       implies that every set is well-orderable, so this is an AC equivalent.
       This function is not strictly the beth function, but rather beth_A is
       the same as ` ( card `` ( R1 `` ( om +o A ) ) ) ` , since conventionally
       we start counting at the first infinite level, and ignore the finite
       levels. $)
    smobeth $p |- Smo ( card o. R1 ) $=
      ( vx vy ccrd cr1 ccom con0 cvv wf cardf wfn mp2an cv wcel wa wb fvex wceq
      cfv fvco3 mp3an12 r1fnon dffn2 mpbi fco ordon wel csdm wbr r1sdom adantll
      cardsdom sylibr wfun ffun ax-mp eleq12 syl2an adantr mpbird ex fdmi issmo
      ) ABCDEZFGFCHZFGDHZFFVCHIDFJVEUAFDUBUCZFGFCDUDKZUEALZFMZBLZFMZNZABUFZVHVC
      RZVJVCRZMZVLVMNZVPVHDRZCRZVJDRZCRZMZVQVRVTUGUHZWBVKVMWCVIVJVHUIUJVRGMVTGM
      WBWCOVHDPVJDPVRVTGGUKKULVLVPWBOZVMVIVNVSQZVOWAQZWDVKCUMZVEVIWEVDWGIGFCUNU
      OZVFFGVHCDSTWGVEVKWFWHVFFGVJCDSTVNVSVOWAUPUQURUSUTFFVCVGVAVB $.
      $( [6-Jun-2013] $)
  $}

$( I decided to comment out the Card class for now.  We have too many
   definitions, and this one doesn't really buy us much. $)

$(
  @c Card @. @( Class of all cardinal numbers @)

  @( Extend class definition to include the class of all cardinal numbers. @)
  ccdn @a class Card @.

  @( Define the class of all cardinal numbers.  The notation "Card" is used
     in Exercise 5(G) of [JustWeese] p. 174.  It should not be confused with
     the lower-case "card" for the cardinal number function ~ df-card .  @)
  df-cardn @a |- Card = ( om u. ran aleph ) @.

  @( Membership in the class of cardinal numbers. @)
  elcard @p |- ( A e. Card <-> ( card ` A ) = A ) @=
    ( ccdn wcel com cale crn cun ccrd cfv wceq df-cardn eleq2i iscard3 bitr4i )
    ABCADEFGZCAHIAJBOAKLAMN @.
    @( [31-Dec-2004] @) @( [23-Sep-2004] @)

  @( The cardinality of a set is a cardinal number. @)
  cardel @p |- ( card ` A ) e. Card @=
    ( ccrd cfv ccdn wcel wceq cardidm elcard mpbir ) ABCZDEJBCJFAGJHI @.
    @( [16-Jan-2005] @) @( [23-Sep-2004] @)

  @( A natural number is a cardinal number. @)
  nncard @p |- ( A e. om -> A e. Card ) @=
    ( com wcel ccrd cfv wceq ccdn cardnn elcard sylibr ) ABCADEAFAGCAHAIJ @.
    @( [16-Jan-2005] @) @( [23-Sep-2004] @)

  @( An alternate definition of the class of all cardinal numbers. @)
  cardnum2 @p |- Card = { x | ( card ` x ) = x } @=
    ( ccdn com cale crn cun cv ccrd cfv wceq cab df-cardn wcel iscard3 bicomi
    abbi2i eqcomi eqtr4i ) BCDEFZAGZHITJZAKZLSUBUAASUATSMTNOPQR @.
    @( [25-Jan-2005] @) @( [23-Sep-2004] @)

  @( The class of cardinal numbers is a proper class.  Exercise 5(G)(b) of
     [JustWeese] p. 174. @)
  cardnprc @p |- -. Card e. _V @=
    ( vx ccdn cvv wcel cv ccrd cfv wceq cab cardprc cardnum2 eleq1i mtbir ) BCD
    AEZFGNHAIZCDAJBOCAKLM @.
    @( [25-Jan-2005] @) @( [23-Sep-2004] @)

  @( The class of transfinite cardinals (the range of the aleph function) is a
     proper class. @)
  alephprc2 @p |- -. ran aleph e. _V @=
    ( cale crn cvv wcel com cun ccdn cardnprc df-cardn eleq1i mtbi omex wa
    unexb biimpi mpan mto ) ABZCDZERFZCDZGCDUAHGTCIJKECDZSUALUBSMUAERNOPQ @.
    @( [19-Feb-2005] @) @( [23-Sep-2004] @)
$)


