$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
         ZFC Axioms with no distinct variable requirements
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  $( A lemma for proving conditionless ZFC axioms. $)
  nd1 $p |- ( A. x x = y -> -. A. x y e. z ) $=
    ( wceq wal wcel elirrv wsbc stdpc4 pm2.21i elequ1 sbie sylib mto ax10o mtoi
    cv ) AQBQZDAERCQZFZAETBEZUASSFZCGZUATBSHUBTBCITUBBCUBUBBEUCJBCCKLMNTABOP $.
    $( [1-Jan-2002] $)

  $( A lemma for proving conditionless ZFC axioms. $)
  nd2 $p |- ( A. x x = y -> -. A. x z e. y ) $=
    ( wceq wal wcel elirrv wsbc stdpc4 pm2.21i elequ2 sbie sylib mto ax10o mtoi
    cv ) AQBQZDAECQZRFZAETBEZUASSFZCGZUATBSHUBTBCITUBBCUBUBBEUCJBCCKLMNTABOP $.
    $( [1-Jan-2002] $)

  $( A lemma for proving conditionless ZFC axioms. $)
  nd3 $p |- ( A. x x = y -> -. A. z x e. y ) $=
    ( cv wceq wal wcel wn ax-4 elirrv elequ2 mtbii con3i 3syl ) ADZBDZEZAFQOPGZ
    HRCFZHQAIQOOGRAJABAKLSRRCIMN $.
    $( [2-Jan-2002] $)

  $( A lemma for proving conditionless ZFC axioms. $)
  nd4 $p |- ( A. x x = y -> -. A. z y e. x ) $=
    ( cv wcel wal wn nd3 alequcoms ) BDADECFGBABACHI $.
    $( [2-Jan-2002] $)

  ${
    $d x v w $.  $d y v w $.  $d z v w $.
    $( A version of the Axiom of Extensionality with no distinct variable
       conditions. $)
    axextnd $p |- E. x ( ( x e. y <-> x e. z ) -> y = z ) $=
      ( vw cv wcel wb wceq wal wex wi wn hbnae dveel2 elequ1 syl6 a9e hbae ax-8
      a4s wa hban adantr adantl hbbid bibi12d cbvald axext3 syl6bir 19.8a eximd
      a1i ex mpi a1d equcomi pm2.61ii 19.35ri ) AEZBEZFZUSCEZFZGZUTVBHZAUSUTHZA
      IZUSVBHZAIZVDAIZVEAJZKZVGLZVILZVLVMVNUAZVJVEVKVOVJDEZUTFZVPVBFZGZDIVEVOVS
      VDDAVMVNAABAMACAMUBZVOVQVRAVTVMVQVQAIKVNABDNUCVNVRVRAIKVMACDNUDUEVPUSHZVS
      VDGKVOWAVQVAVRVCDABODACOUFULUGBCDUHUIVEAUJPUMVGVKVJVGVHAJVKACQVGVHVEAABAR
      VFVHVEKAABCSTUKUNUOVIVKVJVIVFAJVKABQVIVFVEAACARVHVFVEKAVHVFVBUTHVEACBSCBU
      PPTUKUNUOUQUR $.
      $( [14-Aug-2003] $)
  $}

  ${
    $d x z w v $.  $d x y w v $.  $d w ph $.
    $( Lemma for the Axiom of Replacement with no distinct variable
       conditions. $)
    axrepndlem1 $p |- ( -. A. y y = z -> E. x ( E. y A. z ( ph -> z = y ) ->
                 A. z ( z e. x <-> E. x ( x e. y /\ A. y ph ) ) ) ) $=
      ( vw cv wceq wal wn wsbc wi wex wcel wa wb hbnae a1i imbi12d cbvald exbid
      axrep2 ax-17 hbsb3 nd5 hbimd sbequ12r equequ1 a17d dveel2 nalequcoms hbal
      hband hbexd hbbid elequ1 adantl dveeq2 imp hba1 a4s anbi2d exbidv bibi12d
      albid syl ex mpbii ) CFZDFZGCHIZADEFZJZVKVHGZKZEHZCLZVKBFZMZVQVHMZVLCHZNZ
      BLZOZEHZKZBLAVIVHGZKZDHZCLZVIVQMZVSACHZNZBLZOZDHZKZBLVLBCEUAVJWEWPBCDBPZV
      JVPWIWDWOVJVOWHCCDCPVJVNWGEDCDDPZVJVLVMDWRVLVLDHKVJADEAEUBUCZQDCEUDUEVKVI
      GZVNWGOKVJWTVLAVMWFAEDUFZEDCUGRQSTVJWCWNEDWRVJVRWBDWRVJVRDUHVJWADBWQVJVSV
      TDVSVSDHKDCDCBUIUJVTVTDHKVJVLDCWSUKQULUMUNVJWTWCWNOVJWTNZVRWJWBWMWTVRWJOV
      JEDBUOUPXBWAWLBXBVTWKVSXBWTCHZVTWKOVJWTXCCDEUQURXCVLACWTCUSWTVLAOCXAUTVDV
      EVAVBVCVFSRTVG $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.  $d w ph $.
    $( Lemma for the Axiom of Replacement with no distinct variable
       conditions. $)
    axrepndlem2 $p |- ( ( ( -. A. x x = y /\ -. A. x x = z ) /\ -. A. y y = z )
                 -> E. x ( E. y A. z ( ph -> z = y ) ->
                     A. z ( z e. x <-> E. x ( x e. y /\ A. y ph ) ) ) ) $=
      ( vw cv wceq wal wn wa wi wex wcel wb hbnae hbald adantl adantr imdistani
      hban axrepndlem1 ax-17 hbsb3 ax-12 impcom hbimd hbexd dveel1 dveel2 hband
      wsbc a1i hbbid nd5 hba1 simpr ax-4 sbequ12r imbi1d a4s albid syl2an exbid
      syl elequ2 elequ1 sbal2 nalequcoms bicomd sylan9bb adantlr anbi12d cbvexd
      ex bibi12d sylan2 imbi12d syl5ib imp ) BFZCFZGBHIZVTDFZGBHIZJZWAWCGCHIZAW
      CWAGZKZDHZCLZWCVTMZVTWAMZACHZJZBLZNZDHZKZBLZWFABEFZUKZWGKZDHZCLZWCWTMZWTW
      AMZXACHZJZELZNZDHZKZELWEWSXAECDUAWEXLWREBWBWDBBCBOBDBOTZWEXDXKBXMWEXCBCWB
      WDCBCCOBDCOTZWEXBBDWBWDDBCDOBDDOZTZWEXAWGBXMXAXABHKWEABEAEUBUCULZWDWBWGWG
      BHKDCBUDUEUFPUGWEXJBDXPWEXEXIBXMWDXEXEBHKWBBDEUHQWEXHBEWEEUBWEXFXGBWBXFXF
      BHKWDBCEUIRWEXABCXNXQPUJZUGUMPUFWEWTVTGZXLWRNWEXSJZXDWJXKWQXTWEXSCHZJZXDW
      JNWEXSYAWBXSYAKWDCBEUNRSYBXCWICWEYACXNXSCUOTWEWDXSXCWINZYAWBWDUPXSCUQWDXS
      JWDXSDHZJZYCWDXSYDDBEUNZSYEXBWHDWDYDDXOXSDUOZTYDXBWHNZWDXSYHDXSXAAWGAEBUR
      USUTQVAVDVBVCVDXTWEYDJZXKWQNWEXSYDWDXSYDKWBYFQSYIXJWPDWEYDDXPYGTYDWEXSXJW
      PNXSDUQXTXEWKXIWOXSXEWKNWEEBDVEQWEXIWONXSWEXHWNEBXMXRWEXSXHWNNXTXFWLXGWMX
      SXFWLNWEEBCVFQWBXSXGWMNWDWBXGWMBWTUKZXSWMWBYJXGYJXGNCBACBEVGVHVIWMEBURVJV
      KVLVNVMRVOVPVAVDVQVNVMVRVS $.
      $( [2-Jan-2002] $)
  $}

  $( A version of the Axiom of Replacement with no distinct variable
     conditions. $)
  axrepnd $p |- E. x ( E. y A. z ( ph -> z = y ) ->
               A. z ( A. y z e. x <-> E. x ( A. z x e. y /\ A. y ph ) ) ) $=
    ( cv wceq wal wi wex wa wn hban hbae alequcoms intnanrd nexd syl2anc alrimi
    hbnae a1d wcel axrepndlem2 ax-15 com12 nalequcoms imp adantlr ax-4 alequcom
    wb impbid1 con3i syl2an adantll anbi1d exbid bibi12d albid imbi2d mpbid nd2
    exp31 nd3 pm5.21 19.8a syl nd4 nd1 pm2.61iii ) BEZCEZFBGZVJDEZFBGZVKVMFCGZA
    VMVKFZHDGCIZVMVJUAZCGZVJVKUAZDGZACGZJZBIZUJZDGZHZBIZVLKZVNKZVOKZWHWIWJJZWKJ
    ZVQVRVTWBJZBIZUJZDGZHZBIWHABCDUBWMWRWGBWLWKBWIWJBBCBSBDBSLCDBSLZWMWQWFVQWMW
    PWEDWLWKDWIWJDBCDSBDDSLCDDSLWMVRVSWOWDWMVRVSWIWKVRVSHZWJWIWKWTWKWTHCBWKVKVJ
    FCGKWTDBCUCUDUEUFUGVRCUHUKWMWNWCBWSWMVTWAWBWMVTWAWJWKVTWAHZWIWJVMVJFDGZKZVP
    DGZKZXAWKXBVNDBUIULXDVODCUIULXCXEXABCDUCUFUMUNVTDUHUKUOUPUQURUSUPUTVBVLWGWH
    VLWFVQVLWEDBCDMVLVSKZWDKZWEXFCBCBDVANVLWCBBCBMVLWAWBBCDVCOPVSWDVDZQRTWGBVEZ
    VFVNWGWHVNWFVQVNWEDBDDMVNXFXGWEBDCVGVNWCBBDBMVNWAWBWAKZDBDBCVHNOPXHQRTXIVFV
    OWGWHVOWFVQVOWEDCDDMVOXFXGWECDBVHVOWCBCDBMVOWAWBXJDCDCBVANOPXHQRTXIVFVI $.
    $( [2-Jan-2002] $)

  ${
    $d x y w $.  $d x z w $.
    $( Lemma for the Axiom of Union with no distinct variable conditions. $)
    axunndlem1 $p |- E. x A. y ( E. x ( y e. x /\ x e. z ) -> y e. x ) $=
      ( vw cv wceq wal wcel wa wex wi hbae en2lp elequ2 anbi2d mtbii nexd hbnae
      wn a4s pm2.21d a5i 19.8a syl zfun a17d dveel2 hband hbexd hbimd wb elequ1
      anbi1d exbidv imbi12d a1i cbvald exbid mpbii pm2.61i ) BEZCEZFZBGZVAAEZHZ
      VEVBHZIZAJZVFKZBGZAJZVDVKVLVCVJBVDVIVFVDVHABCALVCVHSBVCVFVEVAHZIVHVAVEMVC
      VMVGVFBCANOPTQUAUBVKAUCUDVDSZDEZVEHZVGIZAJZVPKZDGZAJVLADCUEVNVTVKABCARZVN
      VSVJDBBCBRZVNVRVPBWBVNVQBAWAVNVPVGBVNVPBUFZBCAUGUHUIWCUJVOVAFZVSVJUKKVNWD
      VRVIVPVFWDVQVHAWDVPVFVGDBAULZUMUNWEUOUPUQURUSUT $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.
    $( A version of the Axiom of Union with no distinct variable conditions. $)
    axunnd $p |- E. x A. y ( E. x ( y e. x /\ x e. z ) -> y e. x ) $=
      ( vw cv wceq wal wcel wa wex wi wn hbnae hban adantr adantl a4s syl hbae
      wb axunndlem1 ax-17 dveel1 dveel2 hband hbexd hbimd hbald nd5 hba1 elequ2
      imdistani elequ1 anbi12d a1i cbvexd imbi12d albid mpbii elirrv simpl mtoi
      ex syl5ib nexd pm2.21d alrimi 19.8a simpr pm2.61ii ) AEZBEZFZAGZVKCEZFZAG
      ZVLVKHZVKVOHZIZAJZVRKZBGZAJZVNLZVQLZWDWEWFIZVLDEZHZWHVOHZIZDJZWIKZBGZDJWD
      DBCUAWGWNWCDAWEWFAABAMACAMNZWGWMABWEWFBABBMACBMNZWGWLWIAWOWGWKADWGDUBWGWI
      WJAWEWIWIAGKWFABDUCOZWFWJWJAGKWEACDUDPUEZUFWQUGUHWGWHVKFZWNWCTZWGWSIWGWSB
      GZIZWTWGWSXAWEWSXAKWFBADUIOULXBWMWBBWGXABWPWSBUJNXBWLWAWIVRWGWLWATXAWGWKV
      TDAWOWRWSWKVTTKWGWSWIVRWJVSDABUKZDACUMUNUOUPOXAWIVRTZWGWSXDBXCQPUQURRVCUP
      USVCVNWCWDVNWBBABBSVNWAVRVNVTAABASVMVTLZAVMVTVLVLHZBUTVTVRVMXFVRVSVAABBUK
      VDVBQVEVFVGWCAVHZRVQWCWDVQWBBACBSVQWAVRVQVTAACASVPXEAVPVTVOVOHZCUTVTVSVPX
      HVRVSVIACCUMVDVBQVEVFVGXGRVJ $.
      $( [2-Jan-2002] $)
  $}

  $( Lemma for the Axiom of Power Sets with no distinct variable conditions. $)
  axpowndlem1 $p |- ( A. x x = y -> ( -. x = y ->
             E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) ) $=
    ( cv wceq wn wcel wex wal wi pm2.24 a4s ) ADZBDZEZOFMNGCHMCDGBIJAINMGJBIAHZ
    JAOPKL $.
    $( [4-Jan-2002] $)

  ${
    $d x w $.  $d z y w $.
    $( Lemma for the Axiom of Power Sets with no distinct variable
       conditions. $)
    axpowndlem2 $p |- ( -. A. x x = y -> ( -. A. x x = z -> ( -. x = y ->
         E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) ) ) $=
      ( vw cv wceq wal wn wcel wi wa hbnae hban adantr adantl hbald hbimd wb ex
      wex zfpow 19.8a ax-4 imim12i alimi imim1i eximi ax-mp ax-gen dveeq2 ax-17
      a1i hbnd dveel2 hbexd dveel1 equequ1 notbid nd5 imdistani hba1 elequ1 a4s
      exbid adantll albid adantlr imbi12d cbvald elequ2 cbvexd mpbii 19.21bi
      syl ) AEZBEZFZAGHZVOCEZFAGHZVQHZVOVPIZCTZVOVSIZBGZJZAGZVPVOIZJZBGZATZJZVR
      VTKZWLAWMDEZVPFZHZWNVPIZCTZWNVSIZBGZJZDGZVPWNIZJZBGZDTZJZDGWLAGXGDXFWPWQW
      SJZDGZXCJZBGZDTXFDBCUAXKXEDXJXDBXBXIXCXAXHDWQWRWTWSWQCUBWSBUCUDUEUFUEUGUH
      ULUIWMXGWLDAVRVTAABALZACALMZWMWPXFAXMVRWPWPAGJVTVRWOAXLABDUJUMNWMXEADWMDU
      KZWMXDABVRVTBABBLACBLMZWMXBXCAXMWMXAADVRVTDABDLACDLMWMWRWTAXMWMWQACVRVTCA
      BCLACCLMVRWQWQAGJVTABDUNNUOWMWSABXOVTWSWSAGJVRACDUNOPQZPVRXCXCAGJVTABDUPN
      ZQPUOQWMWNVOFZXGWLRWMXRKZWPWAXFWKXRWPWARWMXRWOVQDABUQUROWMXFWKRXRWMXEWJDA
      XMWMXDABXOWMXBXCAXMWMXAADXNXPPXQQPWMXRXEWJRZXSWMXRBGZKZXTWMXRYAVRXRYAJVTB
      ADUSZNUTYBXDWIBWMYABXOXRBVAZMYBXBWGXCWHWMXBWGRYAWMXAWFDAXMXPWMXRXAWFRXSWR
      WCWTWEVTXRWRWCRZVRVTXRKVTXRCGZKYEVTXRYFCADUSUTYFYEVTYFWQWBCXRCVAXRWQWBRCD
      ABVBVCVDOVNVEVRXRWTWERZVTVRXRKVRYAKYGVRXRYAYCUTYAYGVRYAWSWDBYDXRWSWDRBDAC
      VBVCVFOVNVGVHSVINYAXCWHRZWMXRYHBDABVJVCOVHVFVNSVKNVHSVIVLVMS $.
      $( [4-Jan-2002] $)
  $}


  ${
    $d x w $.  $d y z w $.
    $( Lemma for the Axiom of Power Sets with no distinct variable
       conditions. $)
    axpowndlem3 $p |- ( -. x = y ->
               E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) $=
      ( vw cv wceq wal wn wcel wex wi c0 hbnae nalequcoms wb wa adantl syl hbae
      exbid axpowndlem2 axpowndlem1 csn p0ex eleq2 imbi2d albidv 0ex snid eleq1
      cla4ev mpbiri mpg neq0 con1bii imbi1i albii exbii mpbir dveel1 hbexd hbnd
      dveel2 hbimd dveeq2 imdistani hba1 elequ2 notbid elequ1 imbi12d ex cbvald
      a4s mpbii nd3 mtt ax10o alequcoms alnex 3imtr3g sylbird a4sd imim1d alimd
      eximd syl5 a1dd pm2.61d2 pm2.61ii ) AEZBEZFZAGZWKCEZFAGZWMHZWKWLIZCJZWKWO
      IBGZKZAGZWLWKIZKZBGZAJZKZABCUAABCUBZWPWNXGWPWNHZXFWQXIWRAJZHZXCKZBGZAJZWP
      XFXIWKDEZIZAJZHZXOWKIZKZDGZAJZXNYBXOLFZXSKZDGZAJZYCXOLUCZIZKZYFDYEYIDGAYG
      UDWKYGFZYDYIDYJXSYHYCWKYGXOUEUFUGUKYCYHLYGILUHUIXOLYGUJULUMYAYEAXTYDDXRYC
      XSYCXQAXOUNUOUPUQURUSXIYAXMAABAMZXIXTXLDBABBMZXIXRXSBYLXIXQBYLXIXPBAYKXPX
      PBGKBABADUTNVAVBXSXSBGKBABADVCNVDXIXOWLFZXTXLOXIYMPZXRXKXSXCYNXQXJYNXIYMA
      GZPXQXJOZXIYMYOABDVEVFYOYPXIYOXPWRAYMAVGYMXPWROADBAVHVNTQRVIYMXSXCOXIDBAV
      JQVKVLVMTVOWPXMXEAACASWPXLXDBACBSWPXBXKXCWPXAXKAWPXAWSHZXKWPWTHYQXAOACBVP
      WTWSVQRWPWRHZCGZYRAGZYQXKYSYTKCAYRCAVRVSWRCVTWRAVTWAWBWCWDWEWFWGWHXHWIWJ
      $.
      $( [4-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.
    $( Lemma for the Axiom of Power Sets with no distinct variable
       conditions. $)
    axpowndlem4 $p |- ( -. A. y y = x -> ( -. A. y y = z -> ( -. x = y
      -> E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) ) ) $=
      ( vw cv wceq wal wn wcel wex wi wa hbnae adantr hbald hbimd adantl cbvald
      hban wb axpowndlem3 ax-gen dveeq1 ax-17 dveel1 hbexd ax-15 dveel2 equequ2
      hbnd imp notbid nd5 imdistani hba1 ax-4 elequ2 a4s exbid syl sylan2 biidd
      adantll a1i imbi12d albid elequ1 ex mpbii 19.21bi ) BEZAEZFBGHZVKCEZFBGHZ
      VLVKFZHZVLVKIZCJZVLVNIZBGZKZAGZVKVLIZKZBGZAJZKZVMVOLZWHBWIVLDEZFZHZVLWJIZ
      CJZVTDGZKZAGZWJVLIZKZDGZAJZKZDGWHBGXBDADCUAUBWIXBWHDBVMVOBBABMZBCBMSZWIWL
      XABXDVMWLWLBGKVOVMWKBXCBADUCUJNWIWTBAVMVOABAAMBCAMSZWIWSBDWIDUDZWIWQWRBXD
      WIWPBAXEWIWNWOBXDVMWNWNBGKVOVMWMBCBACMBADUEUFNWIVTBDXFVMVOVTWAKACBUGUKZOP
      OVMWRWRBGKVOBADUHNPZOUFPWIWJVKFZXBWHTWIXILZWLVQXAWGXIWLVQTWIXIWKVPDBAUIUL
      QWIXAWGTXIWIWTWFAXEWIWSWEDBXDXHWIXIWSWETXJWQWCWRWDXJWIXIAGZLZWQWCTWIXIXKV
      MXIXKKVOABDUMNUNXLWPWBAWIXKAXEXIAUOSXLWNVSWOWAVOXKWNVSTZVMXKVOXIXMXIAUPVO
      XILVOXICGZLXMVOXIXNCBDUMUNXNXMVOXNWMVRCXICUOXIWMVRTCDBAUQURUSQUTVAVCWIWOW
      ATXKWIVTVTDBXDXGXIVTVTTKWIXIVTVBVDRNVEVFUTXIWRWDTWIDBAVGQVEVHRUSNVEVHRVIV
      JVH $.
      $( [4-Jan-2002] $)
  $}


  ${
    $d x w $.  $d y w $.
    $( A version of the Axiom of Power Sets with no distinct variable
       conditions. $)
    axpownd $p |- ( -. x = y ->
               E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) $=
      ( vw cv wceq wal wn wex wi axpowndlem4 axpowndlem1 alequcoms a1d wa 19.8a
      wcel wb syl alnex hbnae hbae el dveel1 nalequcoms elequ2 a1i cbvexd mpbii
      df-ex sylib adantr biidd dral1 3bitr3g nd2 mtt bitrd dral2 adantl pm2.21d
      hban mtbid alrimi ex pm2.61i pm2.61ii ) BEZAEZFBGVHCEZFBGZVIVHFZHZVIVHQZC
      IZVIVJQBGZJZAGZVHVIQZJZBGZAIZJZABCKWCABABCLZMVLAGZVKWCJWEWCVKWDNWEHZVKWCW
      FVKOZWBVMWGWAWBWGVTBWFVKBABBUAZBCBUBVBWGVRVSWGVNBIZHZAGZVRWFWKHZVKWFWIAIZ
      WLWFWIWMWFVIDEZQZDIWIADUCWFWOVNDBWHWOWOBGJBABADUDUEWNVHFWOVNRJWFDBAUFUGUH
      UIWIAPSWIAUJUKULVKWKVRRWFWJVQBCAVKWJVOHZVQVKVNHZBGWQCGWJWPWQWQBCVKWQUMUNV
      NBTVNCTUOVKVPHWPVQRBCAUPVPVOUQSURUSUTVCVAVDWAAPSNVEVFVG $.
      $( [4-Jan-2002] $)
  $}

$(
  @{
    @d x z @.  @d y z @.
    axpowndNEWlem1 @p  |- ( A. x x = y ->
        ( ( A. x x = y -> E. z A. x ( E. z z e. y -> x e. z ) )
         -> E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) ) @=
      ( weq wal wel wex wi cv csn c0 wceq wn wcel wa en2lp vex snid biantru
      mtbir snnzOLD a1bi mtbi neq0 imbi1i albii bicomi snex eqeq1 notbid eleq1
     imbi12d cla4v sylbi mto hbae wb elequ2 a4s exbidv imbi1d albid mtbii nexdv
     pm2.21d a2i com12 ) ABDZAEZCBFZCGZACFZHZAEZCGZHVIABFCGVLBEHAEBAFHBEAGZVIVO
      VPVIVOVPVIVNCVICAFZCGZVLHZAEZVNVTCIZJZKLZMZWBWANZHZWEWFWEWEWAWBNZOWBWAPWG
      WEWACQZRSTWDWEWAWHUAUBUCVTAIZKLZMZVLHZAEZWFWMVTWLVSAWKVRVLCWIUDUEUFUGWLWF
      AWBWAUHWIWBLZWKWDVLWEWNWJWCWIWBKUIUJWIWBWAUKULUMUNUOVIVSVMAABAUPVIVRVKVLV
      IVQVJCVHVQVJUQAABCURUSUTVAVBVCVDVEVFVG @.
  @}

  @{
    @d x w @.  @d y w @.
   axpowndNEW @p  |- ( ( A. x x = y -> E. z A. x ( E. z z e. y -> x e. z ) ) ->
               E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) @=
?@.
  @}
$)

  $( Lemma for the Axiom of Regularity with no distinct variable conditions. $)
  axregndlem1 $p |- ( A. x x = z -> ( x e. y ->
               E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) ) $=
    ( cv wcel wex wceq wal wn wi wa 19.8a hbae elirrv elequ1 a4s pm2.21d alrimi
    mtbii anim2i expcom eximd syl5 ) ADZBDZEZUFAFUDCDZGZAHZUFUGUDEZUGUEEIZJZCHZ
    KZAFUFALUIUFUNAACAMUFUIUNUIUMUFUIULCACCMUIUJUKUHUJIAUHUDUDEUJANACAOSPQRTUAU
    BUC $.
    $( [3-Jan-2002] $)

  ${
    $d x w $.  $d z y w $.
    $( Lemma for the Axiom of Regularity with no distinct variable
       conditions. $)
    axregndlem2 $p |- ( x e. y ->
                 E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) $=
      ( vw cv wceq wal wcel wn wi wa wex hbnae adantr adantl hbimd wb elequ2 ex
      hban axreg ax-gen dveel2 ax-17 dveel1 ax-15 impcom hbnd hbald hband hbexd
      elequ1 nd5 imdistani imbi1d a4s albid anbi12d cbvexd imbi12d cbvald mpbii
      hba1 syl 19.21bi elirrv mtbii pm2.21d axregndlem1 pm2.61ii ) AEZBEZFZAGZV
      KCEZFAGZVKVLHZVQVOVKHZVOVLHZIZJZCGZKZALZJZVNIZVPIZWEWFWGKZWEAWHDEZVLHZWJV
      OWIHZVTJZCGZKZDLZJZDGWEAGWPDDBCUAUBWHWPWEDAWFWGAABAMACAMTZWHWJWOAWQWFWJWJ
      AGJWGABDUCNZWHWNADWHDUDWHWJWMAWRWHWLACWFWGCABCMACCMZTWHWKVTAWQWGWKWKAGJWF
      ACDUEOWHVSAWQWGWFVSVSAGJCBAUFUGUHPUIUJZUKPWHWIVKFZWPWEQWHXAKWJVQWOWDXAWJV
      QQZWHDABULZOWHWOWDQXAWHWNWCDAWQWTWGXAWNWCQZJWFWGXAXDWGXAKZWJVQWMWBXAXBWGX
      COXEWGXACGZKZWMWBQWGXAXFCADUMUNXGWLWACWGXFCWSXACVCTXFWLWAQZWGXAXHCXAWKVRV
      TDACRUOUPOUQVDURSOUSNUTSVAVBVESVNVQWDVMVQIAVMVKVKHVQAVFABARVGUPVHABCVIVJ
      $.
      $( [3-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.
    $( A version of the Axiom of Regularity with no distinct variable
       conditions. $)
    axregnd $p |- ( x e. y ->
                 E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) $=
      ( vw cv wceq wal wcel wn wi wa wex axregndlem2 hbnae dveel2 adantr adantl
      hban hbnd elequ1 hbimd notbid imbi12d a1i cbvald anbi2d exbid axregndlem1
      wb syl5ib ex alequcoms 19.8a hbae elirrv elequ2 a4s a1d a5i anim2i expcom
      mtbii eximd syl5 pm2.61ii ) CEZAEZFCGZVFBEZFZCGZVGVIHZVLVFVGHZVFVIHZIZJZC
      GZKZALZJZVHIZVKIZVTVLVLDEZVGHZWCVIHZIZJZDGZKZALWAWBKZVSABDMWJWIVRAWAWBACA
      ANCBANRWJWHVQVLWJWGVPDCWAWBCCACNCBCNRZWJWDWFCWKWAWDWDCGJWBCADOPWJWECWKWBW
      EWECGJWACBDOQSUAWCVFFZWGVPUIJWJWLWDVMWFVODCATWLWEVNDCBTUBUCUDUEUFUGUJUKVT
      ACABCUHULVLVLALVKVSVLAUMVKVLVRACBAUNVLVKVRVKVQVLVJVPCVKVOVMVJVOCVJVFVFHVN
      CUOCBCUPVBUQURUSUTVAVCVDVE $.
      $( [3-Jan-2002] $)
  $}

  ${
    $d x w $.  $d z y w $.
    $( Lemma for the Axiom of Infinity with no distinct variable conditions. $)
    axinfndlem1 $p |- ( A. x y e. z -> E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) ) $=
      ( vw cv wceq wal wcel wa wex wi wn a1i hbnae hban wb dveel1 adantr adantl
      hband zfinf ax-15 imp biidd cbvald hbexd hbimd hbald elequ2 nd5 imdistani
      hba1 ax-4 anbi2d a4s exbid imbi12d sylan2 albid adantll anbi12d ex cbvexd
      syl mpbii nd1 pm2.21d nd2 pm2.61ii ) AEZBEZFAGZVJCEZFAGZVKVMHZAGZVKVJHZVQ
      VOVMVJHZIZCJZKZBGZIZAJZKZVLLZVNLZWEWFWGIZVODGZVKDEZHZWKVOVMWJHZIZCJZKZBGZ
      IZDJZKWEWRWIDBCUAMWHWIVPWRWDWHVOVODAWFWGAABANACANOZWFWGVOVPKBCAUBUCZWJVJF
      ZVOVOPKWHXAVOUDMUEWHWQWCDAWSWHWKWPAWFWKWKAGKWGABDQRZWHWOABWFWGBABBNACBNZO
      WHWKWNAWSXBWHWMACWFWGCABCNACCNOWHVOWLAWTWGWLWLAGKWFACDQSTUFUGUHTWHXAWQWCP
      WHXAIZWKVQWPWBXAWKVQPZWHDABUIZSXDWHXABGZIWPWBPZWHXAXGWFXAXGKWGBADUJRUKWGX
      GXHWFWGXGIWOWABWGXGBXCXABULOXGWGXAWOWAPXABUMWGXAIZWKVQWNVTXAXEWGXFSXIWGXA
      CGZIWNVTPZWGXAXJCADUJUKXJXKWGXJWMVSCXACULXAWMVSPCXAWLVRVODACUIUNUOUPSVDUQ
      URUSUTVDVAVBVCUQVEVBVLVPWDABCVFVGVNVPWDACBVHVGVI $.
      $( [5-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.
    $( A version of the Axiom of Infinity with no distinct variable
       conditions. $)
    axinfnd $p |- E. x ( y e. z -> ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) ) $=
      ( vw cv wcel wa wex wi wal wceq wn hbnae hban dveel2 hbald adantl wb a4s
      ex axinfndlem1 ax-gen adantr ax-17 ax-15 impcom hband hbexd nd5 imdistani
      hbimd hba1 elequ1 albid anbi1d exbid syl imbi12d adantll cbvald bi2anan9r
      mpbii 19.21bi nd1 alequcoms pm2.21d nd3 pm2.61ii 19.35ri ) BEZCEZFZVJAEZF
      ZVNVLVKVMFZGZCHZIZBJZGZAVJVMKBJZVJVKKBJZVLAJZVTAHZIZWALZWBLZWEWFWGGZWEBWH
      DEZVKFZAJZWIVMFZWLWJVOGZCHZIZDJZGZAHZIZDJWEBJWSDADCUAUBWHWSWEDBWFWGBBABMB
      CBMNZWHWKWRBWTWGWKWKBJIWFWGWJBABCAMZBCDOZPQWHWQBAWFWGABAAMXANZWHWLWPBWFWL
      WLBJIWGBADOUCZWHWOBDWHDUDWHWLWNBWTXDWHWMBCWFWGCBACMBCCMNWHWJVOBWGWJWJBJIW
      FXBQWGWFVOVOBJICABUEUFUGUHUKZPUGUHUKWHWIVJKZWSWERZWHXFGWHXFAJZGZXGWHXFXHW
      FXFXHIWGABDUIUCUJXIWKWCWRWDXHWKWCRWHXHWJVLAXFAULZXFWJVLRADBCUMZSUNQXIWQVT
      AWHXHAXCXJNXHWLVNWHWPVSXFWLVNRZADBAUMZSWHWOVRDBWTXEWHXFWOVRRZWGXFXNWFWGXF
      GZWLVNWNVQXFXLWGXMQXOWGXFCJZGWNVQRZWGXFXPCBDUIUJXPXQWGXPWMVPCXFCULXFWMVPR
      CXFWJVLVOXKUOSUPQUQURUSTUTVAUPURUQTUTVBVCTWAWCWDWCLABABCVDVEVFWBWCWDBCAVG
      VFVHVI $.
      $( [5-Jan-2002] $)
  $}

  $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
  axacndlem1 $p |- ( A. x x = y ->
            E. x A. y A. z ( A. x ( y e. z /\ z e. w ) -> E. w A. y ( E. w
          ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) ) $=
    ( cv wceq wal wcel wa wex wb hbae simpl alimi nd1 pm2.21d syl5 alrimi 19.8a
    wi syl ) AEZBEZFAGZUCCEZHZUEDEZHZIZAGZUIUCUGHUGUBHIIDJUCUGFKBGDJZTZCGZBGZUN
    AJUDUMBABBLUDULCABCLUJUFAGZUDUKUIUFAUFUHMNUDUOUKABCOPQRRUNASUA $.
    $( [3-Jan-2002] $)

  $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
  axacndlem2 $p |- ( A. x x = z ->
            E. x A. y A. z ( A. x ( y e. z /\ z e. w ) -> E. w A. y ( E. w
          ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) ) $=
    ( cv wceq wal wcel wa wex wb hbae simpr alimi nd1 pm2.21d syl5 alrimi 19.8a
    wi syl ) AEZCEZFAGZBEZUCHZUCDEZHZIZAGZUIUEUGHUGUBHIIDJUEUGFKBGDJZTZCGZBGZUN
    AJUDUMBACBLUDULCACCLUJUHAGZUDUKUIUHAUFUHMNUDUOUKACDOPQRRUNASUA $.
    $( [3-Jan-2002] $)

  $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
  axacndlem3 $p |- ( A. y y = z ->
            E. x A. y A. z ( A. x ( y e. z /\ z e. w ) -> E. w A. y ( E. w
          ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) ) $=
    ( cv wceq wal wcel wa wex wb hbae simpl alimi nd3 pm2.21d syl5 alrimi a5i
    wi 19.8a syl ) BEZCEZFZBGZUCUDHZUDDEZHZIZAGZUJUCUHHUHAEHIIDJUCUHFKBGDJZTZCG
    ZBGZUOAJUEUNBUFUMCBCCLUKUGAGZUFULUJUGAUGUIMNUFUPULBCAOPQRSUOAUAUB $.
    $( [3-Jan-2002] $)

  ${
    $d x v $.  $d y z w v $.
    $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
    axacndlem4 $p |- E. x A. y A. z ( A. x ( y e. z /\ z e. w ) ->
              E. w A. y ( E. w ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) )
                     <-> y = w ) ) $=
      ( vv cv wceq wal wcel wa wex wb wi wn hbnae hban ax-15 imp hbald adantl
      imim1i 2alimi eximi ax-mp ax-17 impcom adantrr adantrl hband dveel1 hbexd
      zfac ax-4 ax-12 hbbid hbimd nd5 sylan9 hba1 biidd a1i cbvald adantr alimi
      hba2 alimd imdistani elequ2 anbi2d a4s exbid bibi1d albid adantll imbi12d
      syl5 syl syld cbvexd mpbii exp32 axacndlem2 axacndlem1 hbae simpr pm2.21d
      ex nd2 alrimi 19.8a pm2.61iii ) AFZCFZGAHZWLBFZGAHZWLDFZGAHZWOWMIZWMWQIZJ
      ZAHZXAWOWQIZWQWLIZJZJZDKZWOWQGZLZBHZDKZMZCHZBHZAKZWNNZWPNZWRNZXOXPXQXRJZJ
      ZXAEHZXAXCWQEFZIZJZJZDKZXHLZBHZDKZMZCHZBHZEKZXOXAYIMZCHBHZEKYMEBCDULYOYLE
      YNYJBCYAXAYIXAEUMUAUBUCUDXTYLXNEAXPXSAACAOXQXRAABAOADAOPPZXTYKABXPXSBACBO
      XQXRBABBOADBOZPPZXTYJACXPXSCACCOXQXRCABCOZADCOPPZXTYAYIAYPXTXAAEXTEUEXTWS
      WTAXPXQWSWSAHMZXRXQXPUUABCAQUFUGXPXRWTWTAHZMZXQXPXRUUCCDAQRUHUIZSXTYHADXP
      XSDACDOXQXRDABDOADDOPPZXTYGABYRXTYFXHAYPXTYEADUUEXTXAYDAUUDXSYDYDAHMXPXSX
      CYCAXQXRXCXCAHMBDAQRXRYCYCAHMXQADEUJTUITUIUKXSXHXHAHMZXPXQXRUUFBDAUNRTUOS
      UKUPSSXTYBWLGZUUGCHZBHZYLXNLZXPXQUUGUUIMXRXPUUGUUHXQUUICAEUQXQUUGBCYSBAEU
      QSURUGXTUUIUUJXTUUIJZYKXMBXTUUIBYRUUHBUSPUUKYJXLCXTUUICYTUUGCBVEPUUKYAXBY
      IXKXTYAXBLUUIXTXAXAEAYPUUDUUGXAXALMXTUUGXAUTVAVBVCXSUUIYIXKLZXPXSUUIJXSUU
      GDHZBHZJUULXSUUIUUNXRUUIUUNMXQUUIUUGBHXRUUNUUHUUGBUUGCUMVDXRUUGUUMBYQDAEU
      QVFVPTVGUUNUULXSUUNYHXJDUUGDBVEUUNYGXIBUUMBUSUUMYGXILBUUMYFXGXHUUMYEXFDUU
      GDUSUUGYEXFLDUUGYDXEXAUUGYCXDXCEADVHVIVIVJVKVLVJVMVKTVQVNVOVMVMWGVRVSVTWA
      ABCDWBABCDWCWRXNXOWRXMBADBWDWRXLCADCWDXBUUBWRXKXAWTAWSWTWEVDWRUUBXKADCWHW
      FVPWIWIXNAWJVQWK $.
      $( [8-Jan-2002] $)
  $}

  ${
    $d x v $.  $d y v $.  $d z w v $.
    $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
    axacndlem5 $p |- E. x A. y A. z ( A. x ( y e. z /\ z e. w ) ->
              E. w A. y ( E. w ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) )
                     <-> y = w ) ) $=
      ( vv cv wceq wal wcel wa wex wb wi wn hbnae hban adantr hband adantl syl
      axacndlem4 dveel2 ax-15 imp adantrl hbald ax-17 impcom hbexd dveeq2 hbbid
      ad2antll hbimd nd5 imdistani hba1 ax-4 elequ1 anbi1d albid sylan2 adantlr
      a4s adantll anbi12d exbid equequ1 bibi12d cbvald imbi12d mpbii axacndlem3
      ex exp32 axacndlem1 alequcoms hbae en2lp elequ2 anbi2d mtbii pm2.21d a4sd
      alrimi a5i 19.8a pm2.61iii ) BFZCFZGBHZWHAFZGBHZWHDFZGZBHZWHWIIZWIWMIZJZA
      HZWRWHWMIZWMWKIZJZJZDKZWNLZBHZDKZMZCHZBHZAKZWJNZWLNZWONZXKXLXMXNJZJZEFZWI
      IZWQJZAHZXSXQWMIZXAJZJZDKZXQWMGZLZEHZDKZMZCHZEHZAKXKAECDUAXPYKXJAXLXOABCA
      OXMXNABAAOBDAOPPZXPYJXIEBXLXOBBCBOXMXNBBABOBDBOPPZXPYIBCXLXOCBCCOXMXNCBAC
      OBDCOPPZXPXTYHBYMXPXSBAYLXPXRWQBXLXRXRBHMXOBCEUBQXLXNWQWQBHMZXMXLXNYOCDBU
      CUDUERZUFXPYGBDXLXODBCDOXMXNDBADOBDDOPPZXPYFBEXPEUGXPYDYEBYMXPYCBDYQXPXSY
      BBYPXPYAXABXNYAYABHMXLXMBDEUBULXOXAXABHMZXLXNXMYRDABUCUHSRRUIXNYEYEBHMXLX
      MBDEUJULUKZUFUIUMUFXPXQWHGZYJXILZXPYTJZXPYTCHZJZUUAXPYTUUCXLYTUUCMXOCBEUN
      QUOUUDYIXHCXPUUCCYNYTCUPPUUDXTWSYHXGXOUUCXTWSLZXLXMUUCUUEXNUUCXMYTUUEYTCU
      QXMYTJXMYTAHZJUUEXMYTUUFABEUNUOUUFUUEXMUUFXSWRAYTAUPYTXSWRLAYTXRWPWQEBCUR
      USZVCUTSTVAVBVDXPYHXGLUUCXPYGXFDYQXPYFXEEBYMYSXPYTYFXELUUBYDXDYEWNXOYTYDX
      DLZXLXNYTUUHXMXNYTJXNYTDHZJUUHXNYTUUIDBEUNUOUUIUUHXNUUIYCXCDYTDUPYTYCXCLD
      YTXSWRYBXBUUGYTYAWTXAEBDURUSVEVCVFSTVDVDYTYEWNLXPEBDVGSVHVMVIVFQVJUTTVMVI
      VFVKVNABCDVLXKABABCDVOVPWOXJXKWNXIBWOXHCBDCVQWOWRXGAWOWRXGWNWRNBWNWPWIWHI
      ZJWRWHWIVRWNUUJWQWPBDCVSVTWAVCWBWCWDWEXJAWFTWG $.
      $( [3-Jan-2002] $)
  $}

  ${
    $d x v $.  $d y v $.  $d z v $.  $d w v $.
    $( A version of the Axiom of Choice with no distinct variable
       conditions. $)
    axacnd $p |- E. x A. y A. z ( A. x ( y e. z /\ z e. w ) -> E. w A. y
       ( E. w ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) $=
      ( vv cv wceq wal wcel wa wex wb wi wn hbnae hban hband hbald adantl nd5
      axacndlem5 dveel1 ad2antrl dveel2 ad2antll ax-15 imp impcom adantrl hbexd
      ax-12 hbbid hbimd imdistani hba1 elequ2 elequ1 anbi12d a4s adantlr sylan9
      albid hba2 anbi1d exbid bibi1d adantll imbi12d ex cbvald mpbii axacndlem2
      syl exp32 alequcoms axacndlem3 hbae simpr alimi pm2.21d syl5 alrimi 19.8a
      nd3 a5i pm2.61iii ) CFZAFZGCHZWGBFZGCHZWGDFZGZCHZWJWGIZWGWLIZJZAHZWQWJWLI
      ZWLWHIZJZJZDKZWJWLGZLZBHZDKZMZCHZBHZAKZWINZWKNZWNNZXKXLXMXNJZJZWJEFZIZXQW
      LIZJZAHZXTXAJZDKZXDLZBHZDKZMZEHZBHZAKXKABEDUAXPYIXJAXLXOACAAOXMXNACBAOCDA
      OPPZXPYHXIBXLXOBCABOXMXNBCBBOCDBOZPPZXPYGXHECXLXOCCACOXMXNCCBCOCDCOPPZXPY
      AYFCYMXPXTCAYJXPXRXSCXMXRXRCHMXLXNCBEUBUCXNXSXSCHMXLXMCDEUDUEQZRXPYECDXLX
      ODCADOXMXNDCBDOCDDOPPZXPYDCBYLXPYCXDCYMXPYBCDYOXPXTXACYNXPWSWTCXOWSWSCHMZ
      XLXMXNYPBDCUFUGSXLXNWTWTCHMZXMXNXLYQDACUFUHUIQQUJXOXDXDCHMZXLXMXNYRBDCUKU
      GSULRUJUMXPXQWGGZYGXHLXPYSJYAWRYFXGXLYSYAWRLZXOXLYSJXLYSAHZJYTXLYSUUAACET
      UNUUAYTXLUUAXTWQAYSAUOYSXTWQLZAYSXRWOXSWPECBUPECDUQURZUSVBSVMUTXOYSYFXGLZ
      XLXOYSJXOYSBHZDHZJUUDXOYSUUFXMYSUUEXNUUFBCETXNYSDBYKDCETRVAUNUUFUUDXOUUFY
      EXFDUUEDUOZUUFYDXEBYSBDVCUUFYCXCXDUUFYBXBDUUGUUFXTWQXAUUEUUBDYSUUBBUUCUSU
      SVDVEVFVBVESVMVGVHVIVJVBVEVKVNXKACABCDVLVOXKBCABCDVPVOWNXJXKWNXIBCDBVQWMX
      HCWRWPAHZWNXGWQWPAWOWPVRVSWNUUHXGCDAWDVTWAWEWBXJAWCVMWF $.
      $( [3-Jan-2002] $)
  $}

  ${
    $d x y z w v u t $.
    $( Axiom of Extensionality ~ ax-ext , reproved from conditionless ZFC
       version and predicate calculus. $)
    zfcndext $p |- ( A. z ( z e. x <-> z e. y ) -> x = y ) $=
      ( cv wcel wb wceq axextnd 19.36aiv ) CDZADZEJBDZEFKLGCCABHI $.
      $( [15-Aug-2003] $)

    $( Axiom of Replacement ~ ax-rep , reproved from conditionless ZFC
       axioms. $)
    zfcndrep $p |- ( A. w E. y A. z ( A. y ph -> z = y ) ->
                     E. y A. z ( z e. y <-> E. w ( w e. x /\ A. y ph ) ) ) $=
      ( wal cv wceq wi wex wcel wa wb hbe1 ax-17 hba1 hbex hbbi 19.3 exbii hban
      hbal hbim elequ2 anbi1d exbidv bibi2d albidv imbi2d axrepnd bibi12i albii
      anbi1i imbi2i mpbi chvar 19.35i anbi2i a1i bibi12d cbvex sylib ) ACFZDGZC
      GZHIDFZCJZEFVDEGZKZVHBGZKZVCCFZLZEJZMZDFZEJVDVEKZVKVCLZEJZMZDFZCJVGVPEVGV
      IVHVEKZVLLZEJZMZDFZIZEJZVGVPIZEJCBWICEVGVPCVFCNVOCDVIVNCVICOZVMCEVKVLCVKC
      OVCCPUAQRUBZUCQVEVJHZWGWIEWLWFVPVGWLWEVODWLWDVNVIWLWCVMEWLWBVKVLCBEUDUEUF
      UGUHUIUFVGVICFZWBDFZVLLZEJZMZDFZIZEJWHVCECDUJWSWGEWRWFVGWQWEDWMVIWPWDVICW
      JSWOWCEWNWBVLWBDWBDOSUMTUKULUNTUOUPUQVPWAECWKVTEDVQVSEVQEOVRENRUBVHVEHZVO
      VTDWTVIVQVNVSECDUDVNVSMWTVMVREVLVCVKVCCACPSURTUSUTUHVAVB $.
      $( [15-Aug-2003] $)

    $( Axiom of Union ~ ax-un , reproved from conditionless ZFC axioms. $)
    zfcndun $p |- E. y A. z ( E. w ( z e. w /\ w e. x ) -> z e. y ) $=
      ( cv wcel wa wex wi axunnd wceq elequ2 elequ1 anbi12d cbvexv imbi1i albii
      wal exbii mpbir ) CEZDEZFZUBAEZFZGZDHZUABEZFZIZCRZBHUIUHUDFZGZBHZUIIZCRZB
      HBCAJUKUPBUJUOCUGUNUIUFUMDBUBUHKUCUIUEULDBCLDBAMNOPQST $.
      $( [15-Aug-2003] $)

    $( Axiom of Power Sets ~ ax-pow , reproved from conditionless ZFC axioms.
       The proof uses the "Axiom of Twoness," ~ dtru . $)
    zfcndpow $p |- E. y A. z ( A. w ( w e. z -> w e. x ) -> z e. y ) $=
      ( cv wcel wi wal wceq wn dtru exnal mpbir hbe1 axpownd albii imbi1i exbii
      wex elequ1 exlimi ax-mp 19.9v ax-17 19.3 imbi12i mpbi imbi12d cbvalv ) DE
      ZCEZFZUJAEZFZGZDHZUKBEZFZGZCHZBSUQUKFZUQUMFZGZBHZURGZCHZBSZVAASZVBCHZGZBH
      ZURGZCHZBSZVGUQUKIZJZBSZVNVQVOBHJBCKVOBLMVPVNBVMBNBCAOUAUBVMVFBVLVECVKVDU
      RVJVCBVHVAVIVBVAAUCVBCVBCUDUEUFPQPRUGUTVFBUSVECUPVDURUOVCDBUJUQIULVAUNVBD
      BCTDBATUHUIQPRM $.
      $( [15-Aug-2003] $)

    $( Axiom of Regularity ~ ax-reg , reproved from conditionless ZFC
       axioms. $)
    zfcndreg $p |- ( E. y y e. x ->
                 E. y ( y e. x /\ A. z ( z e. y -> -. z e. x ) ) ) $=
      ( cv wcel wn wi wal wa wex hbe1 axregnd exlimi ) BDZADZEZPCDZNEQOEFGCHIZB
      JBRBKBACLM $.
      $( [15-Aug-2003] $)

    $( Axiom of Infinity ~ ax-inf , reproved from conditionless ZFC axioms.
       Since we have already reproved Extensionality, Replacement, and Power
       Sets above, we are justified in referencing theorem ~ el in the
       proof. $)
    zfcndinf $p |- E. y ( x e. y /\
                 A. z ( z e. y -> E. w ( z e. w /\ w e. y ) ) ) $=
      ( cv wcel wa wex wi wal el ax-17 hbe1 hbim hbal hban hbex 19.37aiv elequ1
      axinfnd exlimi ax-mp wceq anbi1d exbidv imbi12d cbvalv anbi2i exbii mpbir
      ) AEZBEZFZCEZULFZUNDEZFZUPULFZGZDHZIZCJZGZBHUMUMUKUPFZURGZDHZIZAJZGZBHZVD
      DHVJADKVDVJDVIDBUMVHDUMDLZVGDAUMVFDVKVEDMNOPQVDVIBBADTRUAUBVCVIBVBVHUMVAV
      GCAUNUKUCZUOUMUTVFCABSVLUSVEDVLUQVDURCADSUDUEUFUGUHUIUJ $.
      $( [15-Aug-2003] $)

    $( Axiom of Choice ~ ax-ac , reproved from conditionless ZFC axioms. $)
    zfcndac $p |- E. y A. z A. w ( ( z e. w /\ w e. x ) -> E. v A. u ( E. t
              ( ( u e. w /\ w e. t ) /\ ( u e. t /\ t e. y ) ) <-> u = v ) ) $=
      ( cv wcel wa wex wceq wb wal wi 2albii exbii elequ2 elequ1 anbi12d axacnd
      19.3 imbi1i mpbi equequ2 bibi2d anbi2d cbvexv bibi1i syl6bb albidv anbi1d
      ax-17 exbidv equequ1 bibi12d cbvalv imbi2i mpbir ) CHZDHZIZVAAHZIZJZFHZVA
      IZVAGHZIZJZVFVHIZVHBHZIZJZJZGKZVFEHZLZMZFNZEKZOZDNCNZBKVEVEUTVCIZVCVLIZJZ
      JZAKZUTVCLZMZCNZAKZOZDNCNZBKZVEBNZWLOZDNCNZBKWOBCDAUAWRWNBWQWMCDWPVEWLVEB
      VEBUMUBUCPQUDWCWNBWBWMCDWAWLVEVTWKEAVQVCLZVTVGVDJZVFVCIZWEJZJZAKZVFVCLZMZ
      FNWKWSVSXFFWSVSVPXEMXFWSVRXEVPEAFUEUFVPXDXEVOXCGAVHVCLZVJWTVNXBXGVIVDVGGA
      DRUGXGVKXAVMWEGAFRGABSTTUHUIUJUKXFWJFCVFUTLZXDWHXEWIXHXCWGAXHWTVEXBWFXHVG
      VBVDFCDSULXHXAWDWEFCASULTUNFCAUOUPUQUJUHURPQUS $.
      $( [15-Aug-2003] $)
  $}

$(
  @{
    @d x y z w @.
    zfcndpowNEW @p  |- E. x A. y ( A. x ( x e. y -> x e. z ) -> y e. x ) @=
     ( wel wi wal wex weq wa wb zfcndrep hbae 19.9rOLD equid a1bi albii exbii
       bitri
      imbi1i mpbir 19.37 mpbi ax-gen biantru bibi2i bicomi bi2 sylbi alimi
      eximi syl axpowndNEW ax-mp 19.9rvOLD ax-17 19.3rOLD imbi12i )
      ABDZACDZEZAFZBADZE
      ZBFZAGURCGZUSBFZEZAFZVBEZBFZAGZABHZAFZCBDZCGZUSEZAFZCGZEVKVMUSVNAAHZBFZIZ
      CGZJZAFZCGZVRVMWDEZCGZVMWEEWGVSVLEZAFZBGZWDEZCGVSCBAKWFWKCVMWJWDVMVMBGWJV
      MBABBLMVMWIBVLWHAVSVLANZOPQRSQTVMWDCABCLUAUBWDVQCWCVPAWCUSVOJZVPWMWCVOWBU
      SVNWACVTVNVSBWLUCUDQUEUFUSVOUGUHUIUJUKABCULUMVDVJAVCVIBVAVHVBUTVGAURVEUSV
      FURCUNUSBUSBUOUPUQPSPQT @.
  @}
$)

