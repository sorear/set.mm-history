$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Completeness Axiom and Suprema
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.
    $( A finite set of real numbers has a maximum.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    fimaxre $p |- ( ( A C_ RR /\ A e. Fin /\ A =/= (/) )
                                            -> E. x e. A A. y e. A y <_ x ) $=
      ( cr wss cfn wcel c0 wne cv clt wbr wi wral wrex wor wa ssel wceq wo ltso
      w3a cle mpi fimaxg syl3an1 anim12d imp wb leloe ancoms eqcom orbi2i orcom
      soss neor 3bitri syl6bb biimprd syl anassrs ralimdva reximdva 3ad2ant1
      mpd ) CDEZCFGZCHIZUBAJZBJZIVJVIKLZMZBCNZACOZVJVIUCLZBCNZACOZVFCKPZVGVHVNV
      FDKPVRUACDKUOUDABCKUEUFVFVGVNVQMVHVFVMVPACVFVICGZQVLVOBCVFVSVJCGZVLVOMZVF
      VSVTQZQVIDGZVJDGZQZWAVFWBWEVFVSWCVTWDCDVIRCDVJRUGUHWEVOVLWEVOVKVJVISZTZVL
      WDWCVOWGUIVJVIUJUKWGVKVIVJSZTWHVKTVLWFWHVKVJVIULUMVKWHUNVKVIVJUPUQURUSUTV
      AVBVCVDVE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y $.
    $( A nonempty finite set of real numbers has a maximum.  (Contributed by
       Jeff Madsen, 27-May-2011.)  (Revised by Mario Carneiro, 13-Feb-2014.) $)
    fimaxre2 $p |- ( ( A C_ RR /\ A e. Fin ) ->
                     E. x e. RR A. y e. A y <_ x ) $=
      ( cr wss cfn wcel wa cv cle wbr wral wrex c0 wceq wi cc0 0re rzal breq2
      ralbidv rcla4ev sylancr a1i fimaxre 3expia ssrexv adantr syld pm2.61dne
      wne ) CDEZCFGZHZBIZAIZJKZBCLZADMZCNCNOZUSPUNUTQDGUOQJKZBCLZUSRVABCSURVBAQ
      DUPQOUQVABCUPQUOJTUAUBUCUDUNCNUKZURACMZUSULUMVCVDABCUEUFULVDUSPUMURACDUGU
      HUIUJ $.
      $( [13-Feb-2014] $) $( [27-May-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x z B $.
    $( A nonempty finite set of real numbers has a maximum (image set
       version).  (Contributed by Mario Carneiro, 13-Feb-2014.) $)
    fimaxre3 $p |- ( ( A e. Fin /\ A. y e. A B e. RR )
                                      -> E. x e. RR A. y e. A B <_ x ) $=
      ( vw vz cfn wcel cr wral wa cv cle wbr wceq wrex syl wb wi wal cab r19.29
      eleq1 biimparc rexlimivw ex abssdv abrexfi fimaxre2 syl2anr r19.23v albii
      wss ralcom4 eqeq1 rexbidv ralab 3bitr4i ax-17 breq1 ceqsalg ralbi syl5bbr
      ralimi adantl mpbid ) CGHZDIHZBCJZKELZALZMNZEFLZDOZBCPZFUAZJZAIPZDVKMNZBC
      JZAIPZVIVPIUMVPGHVRVGVIVOFIVIVOVMIHZVIVOKVHVNKZBCPWBVHVNBCUBWCWBBCVNWBVHV
      MDIUCUDUEQUFUGBFCDUHAEVPUIUJVIVRWARVGVIVQVTAIVQVJDOZVLSZETZBCJZVIVTWEBCJZ
      ETWDBCPZVLSZETWGVQWHWJEWDVLBCUKULWEBECUNVOWIVLEFVMVJOVNWDBCVMVJDUOUPUQURV
      IWFVSRZBCJWGVTRVHWKBCVLVSEDIVSEUSVJDVKMUTVAVDWFVSBCVBQVCUPVEVF $.
      $( [13-Feb-2014] $)
  $}

  ${
    $d w x y z v S $.  $d w y A $.
    $( If a set of reals contains a lower bound, it contains a unique lower
       bound. $)
    lbreu $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                E! x e. S A. y e. S x <_ y ) $=
      ( vw cr wss cv cle wbr wral wrex wa wceq wreu wcel breq2 rcla4v im2anan9r
      wi ssel wb anim12d impcom letri3 exbiri com23 syld com3r ralrimivv anim2i
      syl ancoms breq1 ralbidv reu4 sylibr ) CEFZAGZBGZHIZBCJZACKZLVBVADGZUSHIZ
      BCJZLZURVCMZSZDCJACJZLZVAACNVBUQVJUQVIVBUQVHADCCURCOZVCCOZLZVFUQVGVMVFURV
      CHIZVCURHIZLZUQVGSVLVAVNVKVEVOUTVNBVCCUSVCURHPQVDVOBURCUSURVCHPQRVMUQVPVG
      VMUQVGVPVMUQLUREOZVCEOZLZVGVPUAUQVMVSUQVKVQVLVRCEURTCEVCTUBUCURVCUDUKUEUF
      UGUHUIUJULVAVEADCVGUTVDBCURVCUSHUMUNUOUP $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, it contains a unique lower
       bound that belongs to the set. $)
    lbcl $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                U. { x e. S | A. y e. S x <_ y } e. S ) $=
      ( cr wss cv cle wbr wral wrex wa wreu crab cuni lbreu reucl2 ssrab2 sseli
      wcel 3syl ) CDEAFBFGHBCIZACJKUAACLUAACMZNZUBSUCCSABCOUAACPUBCUCUAACQRT $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, the lower bound is less than
       or equal to all members of the set. $)
    lble $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y /\ A e. S ) ->
                U. { x e. S | A. y e. S x <_ y } <_ A ) $=
      ( vw wcel cr wss cv cle wral wrex crab cuni wreu wceq breq1 ralbidv ax-17
      wbr lbreu cbvreuv sylib hbra1 hbrab hbuni hbeleq ralbid reuuni3 syl breq2
      wa hbbr rcla4 imp sylan2 3impb 3coml ) CDFZDGHZAIZBIZJTZBDKZADLZVDADMZNZC
      JTZUSUTVEVHUTVEULZUSVGVBJTZBDKZVHVIEIZVBJTZBDKZEDOZVKVIVDADOVOABDUAVDVNAE
      DVAVLPVCVMBDVAVLVBJQRUBUCVNVDVKEADVLVAPVMVCBDVLVAVBJQRVLVGPVMVJBDBEVGBEVF
      VDBAEDVCBDUDVLDFBSUEUFZUGVLVGVBJQUHUIUJUSVKVHVJVHBCDBEVGCJVPVLJFBSVLCFBSU
      MVBCVGJUKUNUOUPUQUR $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, the lower bound is its
       infimum.  (Revised by Mario Carneiro, 16-Jun-2013.) $)
    lbinfm $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                 sup ( S , RR , `' < ) = U. { x e. S | A. y e. S x <_ y } ) $=
      ( vv vz vw cr cv cle wbr wral wrex wa clt wn wi wcel wb adantr cvv df-sup
      wss ccnv csup crab cuni wceq wreu lbcl ssel mpd 3expa ssel2 adantlr lenlt
      lble syl2anc mpbid elex syl vex brcnvg sylancl notbid ralrimiva a1d ancrd
      mpbird breq2 rcla4ev syl6 ralrimivw breq1 ralbidv imbi1d anbi12d syl12anc
      wor ltso cnvso mpbi supeu reuuni2 mpbi2and syl5eq ) CGUBZAHBHIJBCKZACLZMZ
      CGNUCZUDDHZEHZWJJZOZECKZWLWKWJJZWLFHZWJJZFCLZPZEGKZMZDGUEUFZWGACUEUFZDEFC
      GWJUAWIXDWLWJJZOZECKZWLXDWJJZWSPZEGKZXCXDUGZWIXDGQZXBDGUHZXGXJMZXKRWIXDCQ
      ZXLABCUIZWFXOXLPWHCGXDUJSUKZWIXBDGLZXMWIXLXGXJXRXQWIXFECWIWLCQZMZXFWLXDNJ
      ZOZXTXDWLIJZYBWFWHXSYCABWLCUPULXTXLWLGQZYCYBRWIXLXSXQSWFXSYDWHCGWLUMUNXDW
      LUOUQURWIXFYBRXSWIXEYAWIXDTQZWLTQXEYARWIXOYEXPXDCUSUTEVAXDWLTTNVBVCVDSVHV
      EZWIXIEGWIXHXOXHMWSWIXHXOWIXOXHXPVFVGWRXHFXDCWQXDWLWJVIVJVKVLZXBXNDXDGWKX
      DUGZWOXGXAXJYHWNXFECYHWMXEWKXDWLWJVMVDVNYHWTXIEGYHWPXHWSWKXDWLWJVIVOVNVPZ
      VJVQDEFGCWJGNVRGWJVRVSGNVTWAWBUTXBXNDGXDYIWCUQYFYGWDWE $.
      $( [16-Jun-2013] $) $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, it contains its infimum. $)
    lbinfmcl $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                 sup ( S , RR , `' < ) e. S ) $=
      ( cr wss cv cle wbr wral wrex clt ccnv csup crab cuni lbinfm lbcl eqeltrd
      wa ) CDEAFBFGHBCIZACJSCDKLMTACNOCABCPABCQR $.
      $( [11-Oct-2005] $)

    $( If a set of reals contains a lower bound, its infmimum is less than or
       equal to all members of the set. $)
    lbinfmle $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y /\ A e. S ) ->
                sup ( S , RR , `' < ) <_ A ) $=
      ( cr wss cle wbr wral wrex wcel w3a clt ccnv csup crab cuni wceq lbinfm
      cv 3adant3 lble eqbrtrd ) DEFZATBTGHBDIZADJZCDKZLDEMNOZUEADPQZCGUDUFUHUIR
      UGABDSUAABCDUBUC $.
      $( [11-Oct-2005] $)
  $}

  ${
    $d x y z w v u t A $.
    $( A non-empty, bounded-above set of reals has a supremum.  Stronger
       version of completeness axiom (it has a slightly weaker antecedent). $)
    sup2 $p |- ( ( A C_ RR /\ A =/= (/) /\
                  E. x e. RR A. y e. A ( y < x \/ y = x ) ) ->
               E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( cr cv clt wbr wceq wral wrex w3a wi wa wcel wex c1 caddc adantr imp wss
      c0 wne wo wn co peano2re a1i ssel ltp1 ancli axlttrn 3expb sylan2 sylan2i
      exp4b com34 pm2.43d breq1 syl5ibrcom adantl jaod ex syl6 ralimdv2 expimpd
      com23 jcad ovex eleq1 ralbidv anbi12d cla4ev exlimdv cbvexv syl6ib df-rex
      a2d breq2 3imtr4g imdistani df-3an 3imtr4i axsup syl ) DEUAZDUBUCZBFZAFZG
      HZWHWIIZUDZBDJZAEKZLZWFWGWJBDJZAEKZLZWIWHGHUEBDJWJWHCFZGHZCDKMBEJNAEKWFWG
      NZWNNXAWQNWOWRXAWNWQWFWNWQMWGWFWIEOZWMNZAPZXBWPNZAPZWNWQWFXDWSEOZWTBDJZNZ
      CPZXFWFXCXJAWFXCWIQRUFZEOZWHXKGHZBDJZNZXJWFXCXLXNXCXLMWFXBXLWMWIUGZSUHWFX
      BWMXNWFXBNZWLXMBDDXQWHDOZWLXMWFXBXRWLXMMZMWFXRXBXSWFXRWHEOZXBXSMDEWHUIXTX
      BXSXTXBNZWJXMWKXTXBWJXMMZXTXBYBXTXBWJXBXMXTXBWJXBXMXBYAWJWIXKGHZXMWIUJZXB
      XTXBXLNWJYCNXMMZXBXLXPUKXTXBXLYEWHWIXKULUMUNUOUPUQURTXBWKXMMXTXBXMWKYCYDW
      HWIXKGUSUTVAVBVCVDVGTVRVEVFVHXIXOCXKWIQRVIWSXKIZXGXLXHXNWSXKEVJYFWTXMBDWS
      XKWHGVSVKVLVMVDVNXIXECAWSWIIZXGXBXHWPWSWIEVJYGWTWJBDWSWIWHGVSVKVLVOVPWMAE
      VQWPAEVQVTSWAWFWGWNWBWFWGWQWBWCABCDWDWE $.
      $( [19-Jan-1997] $)

    $( A version of the completeness axiom for reals. $)
    sup3 $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                  E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a clt wceq wo wa wcel 3anass wn wi
      wb ssel leloe expcom imp31 ralbidva rexbidva anbi2d pm5.32i 3bitr4i sylbi
      syl9 sup2 ) DEFZDGHZBIZAIZJKZBDLZAEMZNZUPUQURUSOKZURUSPQZBDLZAEMZNZUSUROK
      UABDLVDURCIOKCDMUBBELRAEMUPUQVBRZRUPUQVGRZRVCVHUPVIVJUPVBVGUQUPVAVFAEUPUS
      ESZRUTVEBDUPVKURDSZUTVEUCZUPVLURESZVKVMDEURUDVNVKVMURUSUEUFUNUGUHUIUJUKUP
      UQVBTUPUQVGTULABCDUOUM $.
      $( [11-May-2007] $) $( [12-Oct-2004] $)

    $( Lemma for ~ infm3 . $)
    infm3lem $p |- ( x e. RR -> E. y e. RR x = -u y ) $=
      ( cv cr wcel cneg wceq wrex renegcl recn negneg syl eqcomd eqeq2d rcla4ev
      cc negeq syl2anc ) ACZDEZSFZDESUAFZGZSBCZFZGZBDHSITUBSTSPEUBSGSJSKLMUFUCB
      UADUDUAGUEUBSUDUAQNOR $.
      $( [14-Jun-2005] $)

    $( The completeness axiom for reals in terms of infimum: a non-empty,
       bounded-below set of reals has a infimum.  (This theorem is the dual of
       ~ sup3 .) $)
    infm3 $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                  E. x e. RR ( A. y e. A -. y < x /\
                  A. y e. RR ( x < y -> E. z e. A z < y ) ) ) $=
      ( vu vv vw vt cr cv cle wbr wral wrex clt wi wa wcel wal df-ral wss c0 wn
      wne cneg crab wex ssel pm4.71rd exbidv df-rex renegcl eleq1 rexxfr bitr3i
      infm3lem syl6bb n0 rabn0 3bitr4g imbi1d impexp albidv wceq imbi12d ralxfr
      breq2 rexbidv breq1 imbi2d negeq eleq1d elrab imbi1i bitri albii 3bitr4ri
      ralbidv wb leneg ancoms syl5bbr rexbiia bitr4i anbi12d ssrab2 sup3 mp3an1
      ralbidva syl6bi notbid adantrd syl5bb ltneg rexrab anbi2d rexbidva adantl
      sylibrd 3impib ) DIUAZDUBUDZAJZBJZKLZBDMZAINZXDXCOLZUCZBDMZXCXDOLZCJZXDOL
      ZCDNZPZBIMZQZAINZXAXBXGQZEJZFJZOLZUCZFGJZUEZDRZGIUFZMZYAXTOLZYAHJZOLZHYGN
      ZPZFIMZQZEINZXRXAXSYGUBUDZYAXTKLZFYGMZEINZQYPXAXBYQXGYTXAYADRZFUGZYFGINZX
      BYQXAUUBYAIRZUUAQZFUGZUUCXAUUAUUEFXAUUAUUDDIYAUHUIUJUUFUUAFINUUCUUAFIUKUU
      AYFFGYEIIYDULFGUPYAYEDUMUNUOUQFDURYFGIUSUTXAXGYAUEZDRZXCUUGKLZPZFIMZAINZY
      TXAXFUUKAIXAXDDRZXEPZBSXDIRZUUNPZBSZXFUUKXAUUNUUPBXAUUNUUOUUMQZXEPUUPXAUU
      MUURXEXAUUMUUODIXDUHUIZVAUUOUUMXEVBUQVCXEBDTUUKUUNBIMUUQUUNUUJBFUUGIIYAUL
      ZBFUPZXDUUGVDZUUMUUHXEUUIXDUUGDUMZXDUUGXCKVGVEVFUUNBITUOUTVHUULUUHXTUEZUU
      GKLZPZFIMZEINYTUUKUVGAEUVDIIXTULZAEUPZXCUVDVDZUUJUVFFIUVJUUIUVEUUHXCUVDUU
      GKVIVJVRUNYSUVGEIYSUUHYRPZFIMZXTIRZUVGYAYGRZYRPZFSUUDUVKPZFSYSUVLUVOUVPFU
      VOUUDUUHQZYRPUVPUVNUVQYRYFUUHGYAIYDYAVDYEUUGDYDYAVKVLVMZVNUUDUUHYRVBVOVPY
      RFYGTUVKFITVQUVMUVKUVFFIUVMUUDQZYRUVEUUHUUDUVMYRUVEVSYAXTVTWAVJWIWBWCWDUQ
      WEYGIUAYQYTYPYFGIWFEFHYGWGWHWJXAXRUUHUUGXCOLZUCZPZFIMZXCUUGOLZYJUEZDRZUWE
      UUGOLZQZHINZPZFIMZQZAINZYPXAXQUWLAIXAXJUWCXPUWKXAUUMXIPZBSUUOUWNPZBSZXJUW
      CXAUWNUWOBXAUWNUURXIPUWOXAUUMUURXIUUSVAUUOUUMXIVBUQVCXIBDTUWCUWNBIMUWPUWN
      UWBBFUUGIIUUTUVAUVBUUMUUHXIUWAUVCUVBXHUVTXDUUGXCOVIWKVEVFUWNBITUOUTXPUWDX
      LUUGOLZCDNZPZFIMXAUWKXOUWSBFUUGIIUUTUVAUVBXKUWDXNUWRXDUUGXCOVGUVBXMUWQCDX
      DUUGXLOVGVHVEVFXAUWSUWJFIXAUWRUWIUWDXAXLDRZUWQQZCUGXLIRZUXAQZCUGZUWRUWIXA
      UXAUXCCXAUXAUXBXAUWTUXBUWQDIXLUHWLUIUJUWQCDUKUWIUXACINUXDUXAUWHCHUWEIIYJU
      LCHUPXLUWEVDUWTUWFUWQUWGXLUWEDUMXLUWEUUGOVIWEUNUXACIUKUOUTVJVRWMWEVHUWMUU
      HUUGUVDOLZUCZPZFIMZUVDUUGOLZUWIPZFIMZQZEINYPUWLUXLAEUVDIIUVHUVIUVJUWCUXHU
      WKUXKUVJUWBUXGFIUVJUWAUXFUUHUVJUVTUXEXCUVDUUGOVGWKVJVRUVJUWJUXJFIUVJUWDUX
      IUWIXCUVDUUGOVIVAVRWEUNYOUXLEIUVMYHUXHYNUXKYHUUHYCPZFIMZUVMUXHUVNYCPZFSUU
      DUXMPZFSYHUXNUXOUXPFUXOUVQYCPUXPUVNUVQYCUVRVNUUDUUHYCVBVOVPYCFYGTUXMFITVQ
      UVMUXMUXGFIUVSYCUXFUUHUVSYBUXEXTYAWNWKVJWIWBUVMYMUXJFIUVSYIUXIYLUWIUUDUVM
      YIUXIVSYAXTWNWAUUDYLUWIVSUVMYLUWFYKQZHINUUDUWIYFUWFYKGHIYDYJVDYEUWEDYDYJV
      KVLWOUUDUXQUWHHIUUDYJIRQYKUWGUWFYAYJWNWPWQWMWRVEWIWEWCWDUQWSWT $.
      $( [11-May-2007] $) $( [14-Jun-2005] $)

    $( Closure of supremum of a non-empty bounded set of reals. $)
    suprcl $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                  sup ( A , RR , < ) e. RR ) $=
      ( vz cr wss c0 wne cv cle wbr wral wrex w3a clt wn wi wa csup wcel supcl
      sup3 ltso syl ) CEFCGHBIZAIZJKBCLAEMNUFUEOKPBCLUEUFOKUEDIOKDCMQBELRAEMCEO
      SETABDCUBABDECOUCUAUD $.
      $( [13-May-2007] $) $( [12-Oct-2004] $)

    $( A member of a non-empty bounded set of reals is less than or equal to
       the set's upper bound. $)
    suprub $p |- ( ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x )
                 /\ B e. A ) -> B <_ sup ( A , RR , < ) ) $=
      ( vz cr wss c0 wne cv cle wbr wral wrex w3a wcel wa clt wn wi csup syl wb
      sup3 ltso supub imp simp1 sselda suprcl adantr lenlt syl2anc mpbird ) CFG
      ZCHIZBJZAJZKLBCMAFNZOZDCPZQZDCFRUAZKLZVCDRLSZUTVAVEUTURUQRLSBCMUQURRLUQEJ
      RLECNTBFMQAFNVAVETABECUDABEFCDRUEUFUBUGVBDFPVCFPZVDVEUCUTCFDUOUPUSUHUIUTV
      FVAABCUJUKDVCULUMUN $.
      $( [15-May-2007] $) $( [12-Oct-2004] $)
  $}

  ${
    $d A w x y $.  $d A w z $.  $d B w z $.
    $( The supremum of a non-empty bounded set of reals is the least upper
       bound.  (Revised by Mario Carneiro, 6-Sep-2014.) $)
    suprlub $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                      E. x e. RR A. y e. A y <_ x ) /\ B e. RR ) ->
                    ( B < sup ( A , RR , < ) <-> E. z e. A B < z ) ) $=
      ( vw cr wss c0 wne cv cle wbr wral wrex w3a wcel wa clt csup wn wi adantr
      wb sup3 simpl1 simpr ltso suplub2 syl3anc breq2 cbvrexv syl6bb ) DGHZDIJZ
      BKZAKZLMBDNAGOZPZEGQZRZEDGSTSMZEFKZSMZFDOZECKZSMZCDOVAUQUPSMUABDNUPUQSMUP
      VCSMFDOUBBGNRAGOZUNUTVBVEUDUSVHUTABFDUEUCUNUOURUTUFUSUTUGABFGDESUHUIUJVDV
      GFCDVCVFESUKULUM $.
      $( [6-Sep-2014] $) $( [15-Nov-2004] $)

    $( An upper bound is not less than the supremum of a non-empty bounded set
       of reals.  (Revised by Mario Carneiro, 6-Sep-2014.) $)
    suprnub $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                      E. x e. RR A. y e. A y <_ x ) /\ B e. RR ) ->
                    ( -. B < sup ( A , RR , < ) <-> A. z e. A -. B < z ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a wcel wa clt csup wn suprlub
      notbid ralnex syl6bbr ) DFGDHIBJAJKLBDMAFNOEFPQZEDFRSRLZTECJRLZCDNZTUGTCD
      MUEUFUHABCDEUAUBUGCDUCUD $.
      $( [6-Sep-2014] $) $( [15-Nov-2004] $)

    $( The supremum of a non-empty bounded set of reals is less than or equal
       to an upper bound.  (Contributed by Mario Carneiro, 6-Sep-2014.) $)
    suprleub $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                       E. x e. RR A. y e. A y <_ x ) /\ B e. RR ) ->
                  ( sup ( A , RR , < ) <_ B <-> A. z e. A z <_ B ) ) $=
      ( vw cr wss cv cle wbr wral wcel wa clt wn wb adantr lenlt syl2anc c0 wne
      wrex w3a csup suprnub suprcl simpr simpl1 sselda ralbidva 3bitr4d cbvralv
      breq1 syl6bb ) DGHZDUAUBZBIAIJKBDLAGUCZUDZEGMZNZDGOUEZEJKZFIZEJKZFDLZCIZE
      JKZCDLVAEVBOKPZEVDOKPZFDLVCVFABFDEUFVAVBGMZUTVCVIQUSVKUTABDUGRUSUTUHZVBES
      TVAVEVJFDVAVDDMZNVDGMUTVEVJQVADGVDUPUQURUTUIUJVAUTVMVLRVDESTUKULVEVHFCDVD
      VGEJUNUMUO $.
      $( [6-Sep-2014] $) $( [18-Mar-2005] $)
  $}

  ${
    $d A b v w x z $.  $d B b v w x y z $.  $d C b w x $.  $d ph b w $.
    supmul1.1 $e |- C = { z | E. v e. B z = ( A x. v ) } $.
    supmul1.2 $e |- ( ph <-> ( ( A e. RR /\ 0 <_ A /\ A. x e. B 0 <_ x ) /\
        ( B C_ RR /\ B =/= (/) /\ E. x e. RR A. y e. B y <_ x ) ) ) $.
    $( The supremum function distributes over multiplication, in the sense that
       ` A x. ( sup B ) = sup ( A x. B ) ` , where ` A x. B ` is shorthand for
       ` { A x. b | b e. B } ` and is defined as ` C ` below.  This is the
       simple version, with only one set argument; see ~ supmul for the more
       general case with two set arguments.  (Contributed by Mario Carneiro,
       5-Jul-2013.) $)
    supmul1 $p |- ( ph ->
      ( A x. sup ( B , RR , < ) ) = sup ( C , RR , < ) ) $=
      ( vw vb cr clt cle wbr wa wcel cc0 adantr csup cmul co wceq wral wrex vex
      wn cv oveq2 eqeq2d cbvrexv eqeq1 rexbidv syl5bb elab2 wss wne simpr sylbi
      c0 w3a simp1d sselda suprcl syl simpl1 simpl2 jca suprub lemul2a syl31anc
      sylan breq1 syl5ibrcom rexlimdva syl5bi ralrimiv wb remulcl syl2anc ssrdv
      wi eleq1a wex simpr2 isseti rgenw r19.2z sylancl exbii n0 rexcom4 3bitr4i
      ovex sylibr ralbidv rcla4ev 3jca suprleub mpbird cdiv 0re simpl3 rcla4cva
      breq2 a1i letrd ex exlimdv mpd imp mulge0 syl22anc anim1i lelttr prodgt02
      syl3anc ltdivmul gt0ne0 redivcl suprlub mpbid ra4e adantl simplrr adantlr
      syl112anc eqbrtrrd mpdan mpi ad2antrr lenlt mtbird nrexdv pm2.65da eqlelt
      expr eqcomd ) AHMNUAZFGMNUAZUBUCZAYTUUBUDZYTUUBOPZYTUUBNPZUHZQZAUUDUUFAUU
      DKUIZUUBOPZKHUEZAUUIKHUUHHRZUUHFLUIZUBUCZUDZLGUFZAUUIDUIZFEUIZUBUCZUDZEGU
      FZUUODUUHHKUGUUTUUPUUMUDZLGUFUUPUUHUDZUUOUUSUVAELGUUQUULUDUURUUMUUPUUQUUL
      FUBUJUKULUVBUVAUUNLGUUPUUHUUMUMUNUOIUPZAUUNUUILGAUULGRZQZUUIUUNUUMUUBOPZU
      VEUULMRZUUAMRZFMRZSFOPZQZUULUUAOPZUVFAGMUULAGMUQZGVAURZCUIBUIZOPCGUEBMUFZ
      AUVIUVJSUVOOPZBGUEZVBZUVMUVNUVPVBZQZUVTJUVSUVTUSUTZVCVDZAUVHUVDAUVTUVHUWB
      BCGVEVFZTZAUVKUVDAUVIUVJAUWAUVIJUVIUVJUVRUVTVGUTZAUWAUVJJUVIUVJUVRUVTVHUT
      ZVITAUVTUVDUVLUWBBCGUULVJVMZUULUUAFVKVLUUHUUMUUBOVNVOVPVQVRZAHMUQZHVAURZU
      UHUVOOPZKHUEZBMUFZVBZUUBMRZUUDUUJVSAUWJUWKUWNAKHMUUKUUOAUUHMRZUVCAUUNUWQL
      GUVEUUMMRZUUNUWQWCUVEUVIUVGUWRAUVIUVDUWFTZUWCFUULVTWAZUUMMUUHWDVFVPVQZWBA
      UUNKWEZLGUFZUWKAUVNUXBLGUEUXCAUWAUVNJUVSUVMUVNUVPWFUTZUXBLGKUUMFUULUBWOWG
      ZWHUXBLGWIWJUUKKWEZUUOKWEUWKUXCUUKUUOKUVCWKKHWLZUUNLKGWMWNWPZAUWPUUJUWNAU
      VIUVHUWPUWFUWDFUUAVTWAZUWIUWMUUJBUUBMUVOUUBUDUWLUUIKHUVOUUBUUHOXFWQWRWAWS
      ZUXIBKKHUUBWTWAXAAUUEYTFXBUCZUULNPZLGUFZAUUEQZUXKUUANPZUXMUXNUXOUUEAUUEUS
      UXNYTMRZUVHUVISFNPZUXOUUEVSAUXPUUEAUWOUXPUXJBKHVEVFZTZAUVHUUEUWDTZAUVIUUE
      UWFTZUXNUVIUVHSUUAOPZSUUBNPZUXQUYAUXTAUYBUUEAUVNUYBUXDUVNUVDLWEAUYBLGWLAU
      VDUYBLAUVDUYBUVESUULUUASMRZUVEXCXGUWCUWEAUVRUVDSUULOPZAUWAUVRJUVIUVJUVRUV
      TXDUTUVQUYEBUULGUVOUULSOXFXEVMZUWHXHXIXJVQXKTUXNSYTOPZUUEQZUYCAUYGUUEAUWK
      UYGUXHUWKUXFAUYGUXGAUUKUYGKAUUKUYGAUUKQZSUUHYTUYDUYIXCXGAUUKUWQUXAXLAUXPU
      UKUXRTAUUKSUUHOPZUUKUUOAUYJUVCAUUNUYJLGUVEUYJUUNSUUMOPZUVEUVIUVJUVGUYEUYK
      UWSAUVJUVDUWGTUWCUYFFUULXMXNUUHUUMSOXFVOVPVQXLAUWOUUKUUHYTOPZUXJBKHUUHVJV
      MZXHXIXJVQXKXOAUYHUYCWCZUUEAUYDUXPUWPUYNUYDAXCXGUXRUXISYTUUBXPXRTXKFUUAXQ
      XNZYTUUAFXSYHXAUXNUVTUXKMRZUXOUXMVSAUVTUUEUWBTUXNUXPUVIFSURZUYPUXSUYAUXNU
      VIUXQUYQUYAUYOFXTWAYTFYAXRBCLGUXKYBWAYCUXNUXLLGUXNUVDQZUXLYTUUMNPZUYRUUMY
      TOPZUYSUHZAUVDUYTUUEUVEUXBUYTUXEUVEUUNUYTKAUVDUUNUYTAUVDUUNQZQZUUKUYTVUBU
      UKAVUBUUOUUKUUNLGYDUVCWPYEVUCUUKQUUHUUMYTOAUVDUUNUUKYFAUUKUYLVUBUYMYGYIYJ
      YRXJYKYGUYRUWRUXPUYTVUAVSAUVDUWRUUEUWTYGAUXPUUEUVDUXRYLZUUMYTYMWAYCUYRUXP
      UVGUVIUXQUXLUYSVSVUDAUVDUVGUUEUWCYGAUVIUUEUVDUWFYLUXNUXQUVDUYOTYTUULFXSYH
      YNYOYPVIAUXPUWPUUCUUGVSUXRUXIYTUUBYQWAXAYS $.
      $( [5-Jul-2013] $)
  $}

  ${
    $d A a b v x y w z $.  $d B a b v x y w z $.  $d C a x w $.
    $d ph a b w z $.
    supmul.1 $e |- C = { z | E. v e. A E. b e. B z = ( v x. b ) } $.
    supmul.2 $e |- ( ph <-> ( ( A. x e. A 0 <_ x /\ A. x e. B 0 <_ x ) /\
        ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) /\
        ( B C_ RR /\ B =/= (/) /\ E. x e. RR A. y e. B y <_ x ) ) ) $.
    $( Lemma for ~ supmul . $)
    supmullem1 $p |- ( ph -> A. w e. C
        w <_ ( sup ( A , RR , < ) x. sup ( B , RR , < ) ) ) $=
      ( va cv cr cle wbr wcel wrex wa clt csup cmul co vex oveq1 eqeq2d rexbidv
      wceq cbvrexv eqeq1 2rexbidv syl5bb elab2 wi wss c0 wne w3a simp2bi suprub
      wral cc0 sylan adantrr simp3bi adantrl simp1d sselda simp1l sylbi rcla4cv
      breq2 syl suprcl adantr simp1r lemul12a syl22anc mp2and ex breq1 biimprcd
      imp jca syl6 rexlimdvv syl5bi ralrimiv ) AENZGOUAUBZHOUAUBZUCUDZPQZEIWJIR
      WJMNZJNZUCUDZUIZJHSMGSZAWNDNZFNZWPUCUDZUIZJHSZFGSZWSDWJIEUEXEWTWQUIZJHSZM
      GSWTWJUIZWSXDXGFMGXAWOUIZXCXFJHXIXBWQWTXAWOWPUCUFUGUHUJXHXFWRMJGHWTWJWQUK
      ULUMKUNAWRWNMJGHAWOGRZWPHRZTZWQWMPQZWRWNUOAXLXMAXLTZWOWKPQZWPWLPQZXMAXJXO
      XKAGOUPZGUQURZCNBNZPQZCGVBBOSZUSZXJXOAVCXSPQZBGVBZYCBHVBZTZYBHOUPZHUQURZX
      TCHVBBOSZUSZLUTZBCGWOVAVDVEAXKXPXJAYJXKXPAYFYBYJLVFZBCHWPVAVDVGXNWOORZVCW
      OPQZTWKORZWPORZVCWPPQZTWLORZXOXPTXMUOXNYMYNAXJYMXKAGOWOAXQXRYAYKVHVIVEAXJ
      YNXKAXJYNAYDXJYNUOAYFYBYJUSZYDLYDYEYBYJVJVKYCYNBWOGXSWOVCPVMVLVNWDVEWEAYO
      XLAYBYOYKBCGVOVNVPXNYPYQAXKYPXJAHOWPAYGYHYIYLVHVIVGAXKYQXJAXKYQAYEXKYQUOA
      YSYELYDYEYBYJVQVKYCYQBWPHXSWPVCPVMVLVNWDVGWEAYRXLAYJYRYLBCHVOVNVPWOWKWPWL
      VRVSVTWAWRWNXMWJWQWMPWBWCWFWGWHWI $.
      $( [5-Jul-2013] $)

    $( Lemma for ~ supmul . $)
    supmullem2 $p |- ( ph ->
        ( C C_ RR /\ C =/= (/) /\ E. x e. RR A. w e. C w <_ x ) ) $=
      ( va cr cv cle wral wrex wcel wceq wss c0 wne wbr cmul vex eqeq2d rexbidv
      co oveq1 cbvrexv eqeq1 2rexbidv syl5bb elab2 wa wi cc0 w3a simp2bi simp1d
      sseld simp3bi anim12d remulcl syl6 eleq1a rexlimdvv syl5bi ssrdv wex ovex
      simp2d isseti rgenw r19.2z sylancl rexcom4 sylib ralrimivw n0 exbii bitri
      syl2anc sylibr clt csup suprcl syl supmullem1 breq2 ralbidv rcla4ev 3jca
      ) AINUAIUBUCZEOZBOZPUDZEIQZBNRZAEINWPISZWPMOZJOZUEUIZTZJHRZMGRZAWPNSZDOZF
      OZXCUEUIZTZJHRZFGRZXGDWPIEUFXNXIXDTZJHRZMGRXIWPTZXGXMXPFMGXJXBTZXLXOJHXRX
      KXDXIXJXBXCUEUJUGUHUKXQXOXEMJGHXIWPXDULUMUNKUOZAXEXHMJGHAXBGSZXCHSZUPZXDN
      SZXEXHUQAYBXBNSZXCNSZUPYCAXTYDYAYEAGNXBAGNUAZGUBUCZCOWQPUDZCGQBNRZAURWQPU
      DZBGQYJBHQUPZYFYGYIUSZHNUAZHUBUCZYHCHQBNRZUSZLUTZVAVBAHNXCAYMYNYOAYKYLYPL
      VCZVAVBVDXBXCVEVFXDNWPVGVFVHVIVJAXGEVKZWOAXFEVKZMGRZYSAYGYTMGQUUAAYFYGYIY
      QVMAYTMGAXEEVKZJHRZYTAYNUUBJHQUUCAYMYNYOYRVMUUBJHEXDXBXCUEVLVNVOUUBJHVPVQ
      XEJEHVRVSVTYTMGVPWDXFMEGVRVSWOXAEVKYSEIWAXAXGEXSWBWCWEAGNWFWGZHNWFWGZUEUI
      ZNSZWPUUFPUDZEIQZWTAUUDNSZUUENSZUUGAYLUUJYQBCGWHWIAYPUUKYRBCHWHWIUUDUUEVE
      WDABCDEFGHIJKLWJWSUUIBUUFNWQUUFTWRUUHEIWQUUFWPPWKWLWMWDWN $.
      $( [5-Jul-2013] $)

    $( The supremum function distributes over multiplication, in the sense that
       ` ( sup A ) x. ( sup B ) = sup ( A x. B ) ` , where ` A x. B ` is
       shorthand for ` { a x. b | a e. A , b e. B } ` and is defined as ` C `
       below.  We made use of this in our definition of multiplication in the
       Dedekind cut construction of the reals (see ~ df-mp ).  (Contributed by
       Mario Carneiro, 5-Jul-2013.) $)
    supmul $p |- ( ph ->
      ( sup ( A , RR , < ) x. sup ( B , RR , < ) ) = sup ( C , RR , < ) ) $=
      ( va vw cr wceq cle wbr wrex wcel wral clt csup cmul co wa cv cab wss wne
      c0 w3a cc0 simp2bi suprcl syl simp3bi cc mulcom syl2an syl2anc wex simp2d
      recn n0 sylib 0re a1i simp1d sselda adantr simp1r sylbi breq2 rcla4cv imp
      wi suprub sylan letrd exlimdv mpd simp1l eqid biid supmul1 syl31anc eqtrd
      ex vex eqeq1 rexbidv elab ra4e oveq1 eqeq2d cbvrexv 2rexbidv syl5bb elab2
      sylibr supmullem2 sylan9r syl5bi ralrimiv adantlr eleq1a rexlimdva abssdv
      wb remulcl ovex isseti r19.2z sylancl rexcom4 cbvexv abn0 ralbidv rcla4ev
      rgenw suprleub mpbird eqbrtrd breq1 syl5ibrcom supmullem1 jca letri3 ) AF
      NUAUBZGNUAUBZUCUDZHNUAUBZOZYKYLPQZYLYKPQZUEZAYNYOAYKDUFZYJLUFZUCUDZOZLFRZ
      DUGZNUAUBZYLPAYKYJYIUCUDZUUCAYINSZYJNSZYKUUDOZAFNUHZFUJUIZCUFBUFZPQZCFTBN
      RZUKZUUEAULUUJPQZBFTZUUNBGTZUEZUUMGNUHZGUJUIZUUKCGTBNRZUKZKUMZBCFUNUOZAUV
      AUUFAUUQUUMUVAKUPZBCGUNUOZUUEYIUQSYJUQSZUUGUUFYIVCYJVCZYIYJURUSUTAUUFULYJ
      PQZUUOUUMUUDUUCOUVEAIUFZGSZIVAZUVHAUUSUVKAUURUUSUUTUVDVBZIGVDVEAUVJUVHIAU
      VJUVHAUVJUEZULUVIYJULNSUVMVFVGAGNUVIAUURUUSUUTUVDVHVIZAUUFUVJUVEVJAUVJULU
      VIPQZAUUPUVJUVOVPAUUQUUMUVAUKZUUPKUUOUUPUUMUVAVKVLZUUNUVOBUVIGUUJUVIULPVM
      VNUOVOAUVAUVJUVIYJPQUVDBCGUVIVQVRVSWHVTWAAUVPUUOKUUOUUPUUMUVAWBVLZUVBUUFU
      VHUUOUKUUMUEZBCDLYJFUUBUUBWCUVSWDWEWFWGAUUCYLPQZMUFZYLPQZMUUBTZAUWBMUUBUW
      AUUBSUWAYSOZLFRZAUWBUUAUWEDUWAMWIZYQUWAOZYTUWDLFYQUWAYSWJWKWLAUWDUWBLFAYR
      FSZUEZUWBUWDYSYLPQUWIYSYRYJUCUDZYLPUWIUUFYRNSZYSUWJOZAUUFUWHUVEVJZAFNYRAU
      UHUUIUULUVBVHVIZUUFUVFYRUQSUWLUWKUVGYRVCYJYRURUSUTUWIUWJYQYRUVIUCUDZOZIGR
      ZDUGZNUAUBZYLPUWIUWKULYRPQZUUPUVAUWJUWSOUWNAUWHUWTAUUOUWHUWTVPUVRUUNUWTBY
      RFUUJYRULPVMVNUOVOAUUPUWHUVQVJAUVAUWHUVDVJUWKUWTUUPUKUVAUEZBCDIYRGUWRUWRW
      CUXAWDWEWFUWIUWSYLPQZUWBMUWRTZUWIUWBMUWRUWAUWRSUWAUWOOZIGRZUWIUWBUWQUXEDU
      WAUWFUWGUWPUXDIGYQUWAUWOWJZWKZWLUWHUXEUWAHSZAUWBUWHUXEUXHUWHUXEUEUXELFRZU
      XHUXELFWMYQEUFZUVIUCUDZOZIGRZEFRZUXIDUWAHUWFUXNUWQLFRUWGUXIUXMUWQELFUXJYR
      OZUXLUWPIGUXOUXKUWOYQUXJYRUVIUCWNWOWKWPUWGUWPUXDLIFGUXFWQWRJWSWTWHAHNUHHU
      JUIUWAUUJPQZMHTBNRUKZUXHUWBVPABCDMEFGHIJKXAZUXQUXHUWBBMHUWAVQWHUOXBXCXDZU
      WIUWRNUHUWRUJUIZUXPMUWRTZBNRZYLNSZUXBUXCXIUWIUWQDNUWIUWPYQNSZIGUWIUVJUEZU
      WONSZUWPUYDVPUYEUWKUVINSZUYFUWIUWKUVJUWNVJAUVJUYGUWHUVNXEYRUVIXJUTUWONYQX
      FUOXGXHAUXTUWHAUWQDVAZUXTAUXEMVAZUYHAUXDMVAZIGRZUYIAUUSUYJIGTUYKUVLUYJIGM
      UWOYRUVIUCXKXLXTUYJIGXMXNUXDIMGXOVEUWQUXEDMUXGXPWTUWQDXQWTVJUWIUYCUXCUYBA
      UYCUWHAUXQUYCUXRBMHUNUOZVJZUXSUYAUXCBYLNUUJYLOZUXPUWBMUWRUUJYLUWAPVMZXRXS
      UTUYMBMMUWRYLYAWFYBYCYCUWAYSYLPYDYEXGXCXDZAUUBNUHUUBUJUIZUXPMUUBTZBNRZUYC
      UVTUWCXIAUUADNAYTUYDLFUWIYSNSZYTUYDVPUWIUUFUWKUYTUWMUWNYJYRXJUTYSNYQXFUOX
      GXHAUUADVAZUYQAYTDVAZLFRZVUAAUUIVUBLFTVUCAUUHUUIUULUVBVBVUBLFDYSYJYRUCXKX
      LXTVUBLFXMXNYTLDFXOVEUUADXQWTAUYCUWCUYSUYLUYPUYRUWCBYLNUYNUXPUWBMUUBUYOXR
      XSUTUYLBMMUUBYLYAWFYBYCAYOUWAYKPQMHTZABCDMEFGHIJKYFAUXQYKNSZYOVUDXIUXRAUU
      EUUFVUEUVCUVEYIYJXJUTZBMMHYKYAUTYBYGAVUEUYCYMYPXIVUFUYLYKYLYHUTYB $.
      $( [6-Sep-2014] $) $( [5-Jul-2013] $)
  $}

  ${
    $d x y z A $.
    sup3i.1 $e |- ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) $.
    $( A version of the completeness axiom for reals. $)
    sup3ii $p |- E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a clt wn wi wa sup3 ax-mp ) DFGDHI
      BJZAJZKLBDMAFNOUCUBPLQBDMUBUCPLUBCJPLCDNRBFMSAFNEABCDTUA $.
      $( [14-Mar-2007] $) $( [23-Aug-1999] $)

    $( Closure of supremum of a non-empty bounded set of reals. $)
    suprclii $p |- sup ( A , RR , < ) e. RR $=
      ( vz cr clt ltso sup3ii supcli ) ABEFCGHABECDIJ $.
      $( [15-May-2007] $) $( [12-Sep-1999] $)

    $( A member of a non-empty bounded set of reals is less than or equal to
       the set's upper bound. $)
    suprubii $p |- ( B e. A -> B <_ sup ( A , RR , < ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt csup suprub mpan ) CFGC
      HIBJAJKLBCMAFNODCPDCFQRKLEABCDST $.
      $( [16-May-2007] $) $( [12-Sep-1999] $)

    $d z B $.
    $( The supremum of a non-empty bounded set of reals is the least upper
       bound. $)
    suprlubii $p |- ( B e. RR ->
      ( B < sup ( A , RR , < ) <-> E. z e. A B < z ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt csup wb suprlub mpan )
      DGHDIJBKAKLMBDNAGOPEGQEDGRSRMECKRMCDOTFABCDEUAUB $.
      $( [6-Sep-2014] $) $( [15-Oct-2004] $)

    $( An upper bound is not less than the supremum of a non-empty bounded set
       of reals. $)
    suprnubii $p |- ( B e. RR ->
      ( -. B < sup ( A , RR , < ) <-> A. z e. A -. B < z ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt csup wn wb suprnub mpan
      ) DGHDIJBKAKLMBDNAGOPEGQEDGRSRMTECKRMTCDNUAFABCDEUBUC $.
      $( [6-Sep-2014] $) $( [15-Oct-2004] $)

    $( The supremum of a non-empty bounded set of reals is less than or equal
       to an upper bound. $)
    suprleubii $p |- ( B e. RR ->
      ( sup ( A , RR , < ) <_ B <-> A. z e. A z <_ B ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt csup wb suprleub mpan )
      DGHDIJBKAKLMBDNAGOPEGQDGRSELMCKELMCDNTFABCDEUAUB $.
      $( [6-Sep-2014] $) $( [18-Mar-2005] $)
  $}

  ${
    $d x y z $.  $d z A $.  $d y z ph $.  $d x z ps $.
    reuunineg.1 $e |- ( x = -u y -> ( ph <-> ps ) ) $.
    $( The negative of the unique real such that ` ph ` . $)
    reuunineg $p |- ( E! x e. RR ph ->
                U. { x e. RR | ph } = -u U. { y e. RR | ps } ) $=
      ( vz cr cv cneg crab cuni hbrab1 hbuni hbneg renegcl negeq wcel wceq recn
      cc wb negcon2 syl2an reuhyp reuunixfr ) ABCDFGDHZIZBDGJZKZIDFUIDFUHBDFGLM
      NUFOUIOEUFUIPCDUGCHZIZGUJOUJGQUJTQUFTQUJUGRUFUKRUAUFGQUJSUFSUJUFUBUCUDUE
      $.
      $( [13-Jun-2005] $)
  $}

  ${
    $d x y z w v u t A $.  $d x y z B $.
    $( The infimum (expressed as supremum with converse 'less-than') of a set
       of reals ` A ` . $)
    dfinfmr $p |- ( A C_ RR -> sup ( A , RR , `' < ) = U. { x e. RR |
        ( A. y e. A x <_ y /\ A. y e. RR ( x < y -> E. z e. A z < y ) ) } ) $=
      ( cr wss clt cv wbr wn wral wrex wi wa crab cuni wcel wb vex brcnv df-sup
      ccnv csup ssel2 lenlt notbii syl6rbbr sylan2 ancoms an32s ralbidva rexbii
      cle imbi12i ralbii a1i anbi12d rabbidva unieqd syl5eq ) DEFZDEGUBZUCAHZBH
      ZVBIZJZBDKZVDVCVBIZVDCHZVBIZCDLZMZBEKZNZAEOZPVCVDUMIZBDKZVCVDGIZVIVDGIZCD
      LZMZBEKZNZAEOZPABCDEVBUAVAVOWDVAVNWCAEVAVCEQZNZVGVQVMWBWFVFVPBDVAVDDQZWEV
      FVPRZWEVAWGNZWHWIWEVDEQZWHDEVDUDWEWJNVPVDVCGIZJVFVCVDUEVEWKVCVDGASZBSZTUF
      UGUHUIUJUKVMWBRWFVLWABEVHVRVKVTVDVCGWMWLTVJVSCDVDVIGWMCSTULUNUOUPUQURUSUT
      $.
      $( [9-Oct-2005] $)

    $( The infimum (expressed as supremum with converse 'less-than') of a set
       of reals ` A ` is the negative of the supremum of the negatives of its
       elements.  The antecedent ensures that ` A ` is nonempty and has a lower
       bound. $)
    infmsup $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                   sup ( A , RR , `' < ) =
                 -u sup ( { z e. RR | -u z e. A } , RR , < ) ) $=
      ( vw vv vu vt cr cv wbr wral wrex clt cneg wcel crab wceq wi wa ccnv csup
      wss c0 wne cle infm3 3exp wreu wor ltso cnvso mpbi supeu vex brcnv notbii
      wn ralbii rexbii imbi12i anbi12i reubii 3imtr3i cuni df-sup wb a1i unieqi
      rabbiia eqtri breq2 notbid ralbidv imbi1d anbi12d reuunineg syl5eq eleq1d
      breq1 wal negeq elrab imbi1i impexp bitri df-ral 3bitr4ri imbi2d ralbidva
      albii ltneg syl5bbr ancoms rexrab anbi2d rexbidva syl5bb imbi12d pm4.71rd
      adantl ssel syl6rbb albidv renegcl infm3lem ralxfr bitr3i 3bitr4g rexbidv
      wex adantrd exbidv df-rex rexxfr syl5rbb sylan9bbr rabbidva unieqd negeqd
      eleq1 eqcomd sylan9eqr ex syl5 syl6d 3imp ) DIUCZDUDUEZAJZBJZUFKBDLAIMZDI
      NUAZUBZCJZOZDPZCIQZINUBZOZRZYHYIYLYKYJNKZURZBDLZYJYKNKZEJZYKNKZEDMZSZBILZ
      TZAIMZUUAYHYIYLUULABEDUGUHUULUUKAIUIZYHUUAYJYKYMKZURZBDLZYKYJYMKZYKUUFYMK
      ZEDMZSZBILZTZAIMUVBAIUIUULUUMABEIDYMINUJIYMUJUKINULUMUNUVBUUKAIUUPUUDUVAU
      UJUUOUUCBDUUNUUBYJYKNAUOZBUOZUPUQUSUUTUUIBIUUQUUEUUSUUHYKYJNUVDUVCUPUURUU
      GEDYKUUFNUVDEUOUPUTVAUSVBZUTUVBUUKAIUVEVCVDYHUUMUUAUUMYHYNYKFJZOZNKZURZBD
      LZUVGYKNKZUUHSZBILZTZFIQZVEZOZYTUUMYNUUKAIQZVEZUVQYNUVBAIQZVEUVSABEDIYMVF
      UVTUVRUVBUUKAIUVBUUKVGYJIPUVEVHVJVIVKUUKUVNAFYJUVGRZUUDUVJUUJUVMUWAUUCUVI
      BDUWAUUBUVHYJUVGYKNVLVMVNUWAUUIUVLBIUWAUUEUVKUUHYJUVGYKNVTVOVNVPVQVRYHYTU
      VQYHYSUVPYHYSUVFGJZNKZURZGYRLZUWBUVFNKZUWBHJZNKZHYRMZSZGILZTZFIQZVEUVPFGH
      YRINVFYHUWMUVOYHUWLUVNFIUVFIPZUWLUWBOZDPZUWOUVGNKZURZSZGILZUVGUWONKZUWGOZ
      DPZUXBUWONKZTZHIMZSZGILZTYHUVNUWNUWEUWTUWKUXHUWEUWPUWDSZGILZUWNUWTUWBYRPZ
      UWDSZGWAUWBIPZUXISZGWAUWEUXJUXLUXNGUXLUXMUWPTZUWDSUXNUXKUXOUWDYQUWPCUWBIY
      OUWBRYPUWODYOUWBWBVSWCWDUXMUWPUWDWEWFWKUWDGYRWGUXIGIWGWHUWNUXIUWSGIUWNUXM
      TZUWDUWRUWPUXPUWCUWQUVFUWBWLVMWIWJWMUWNUWJUXGGIUXPUWFUXAUWIUXFUXMUWNUWFUX
      AVGUWBUVFWLWNUXMUWIUXFVGUWNUWIUXCUWHTZHIMUXMUXFYQUXCUWHCHIYOUWGRYPUXBDYOU
      WGWBVSWOUXMUXQUXEHIUXMUWGIPTUWHUXDUXCUWBUWGWLWPWQWRXAWSWJVPYHUWTUVJUXHUVM
      YHYKIPZYKDPZUVISZSZBWAZUXTBWAUWTUVJYHUYAUXTBYHUXTUXRUXSTZUVISUYAYHUXSUYCU
      VIYHUXSUXRDIYKXBWTVOUXRUXSUVIWEXCXDUWTUXTBILUYBUXTUWSBGUWOIIUWBXEZBGXFZYK
      UWORZUXSUWPUVIUWRYKUWODYAUYFUVHUWQYKUWOUVGNVTVMWSXGUXTBIWGXHUVIBDWGXIUVMU
      XAUUFUWONKZEDMZSZGILYHUXHUVLUYIBGUWOIIUYDUYEUYFUVKUXAUUHUYHYKUWOUVGNVLUYF
      UUGUYGEDYKUWOUUFNVLXJWSXGYHUYIUXGGIYHUYHUXFUXAYHUUFDPZUYGTZEXKUUFIPZUYKTZ
      EXKZUYHUXFYHUYKUYMEYHUYKUYLYHUYJUYLUYGDIUUFXBXLWTXMUYGEDXNUXFUYKEIMUYNUYK
      UXEEHUXBIIUWGXEEHXFUUFUXBRUYJUXCUYGUXDUUFUXBDYAUUFUXBUWONVTVPXOUYKEIXNXHX
      IWIVNXPVPXQXRXSVRXTYBYCYDYEYFYG $.
      $( [13-May-2007] $) $( [14-Jun-2005] $)

    $( Closure of infimum of a non-empty bounded set of reals. $)
    infmrcl $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                  sup ( A , RR , `' < ) e. RR ) $=
      ( vv vw vz cr wss c0 wne cv cle wbr wral cneg wcel wa renegcl syl6 wceq
      wrex w3a clt ccnv csup crab infmsup wex n0 ssel cc ssel2 recn negneg 3syl
      simpr eqeltrd jcad negeq eleq1d elrab ne0i sylbir exlimdv sylan2b 3adant3
      ex wi breq2 rcla4va adantll wb lenegcon2 adantrr adantr mpbid exp31 com23
      imp syl5bi ralrimdv jctild ralbidv rexlimiv 3ad2ant3 ssrab2 suprcl mp3an1
      rcla4ev syl2anc syl ) CGHZCIJZAKZBKZLMZBCNZAGUAZUBZCGUCUDUEDKZOZCPZDGUFZG
      UCUEZOZGABDCUGWSXDGPZXEGPWSXCIJZEKZFKZLMZEXCNZFGUAZXFWLWMXGWRWMWLXICPZFUH
      ZXGFCUIWLXNXGWLXMXGFWLXMXIOZGPZXOOZCPZQZXGWLXMXPXRWLXMXIGPZXPCGXIUJXIRSWL
      XMXRWLXMQZXQXICYAXTXIUKPXQXITCGXIULXIUMXIUNUOWLXMUPUQVGURXSXOXCPXGXBXRDXO
      GWTXOTXAXQCWTXOUSUTVAXCXOVBVCSVDVSVEVFWRWLXLWMWQXLAGWNGPZWQWNOZGPZXHYCLMZ
      EXCNZQXLYBWQYFYDYBWQYEEXCYBXHXCPZWQYEYGXHGPZXHOZCPZQZYBWQYEVHXBYJDXHGWTXH
      TXAYICWTXHUSUTVAYBYKWQYEYBYKQZWQQWNYILMZYEYKWQYMYBYJWQYMYHWPYMBYICWOYIWNL
      VIVJVKVKYLYMYEVLZWQYBYHYNYJWNXHVMVNVOVPVQVTVRWAWNRWBXKYFFYCGXIYCTXJYEEXCX
      IYCXHLVIWCWISWDWEXCGHXGXLXFXBDGWFFEXCWGWHWJXDRWKUQ $.
      $( [14-May-2007] $) $( [8-Oct-2005] $)
  $}

  ${
    $d w x y A $.  $d w z A $.  $d w z B $.
    $( Any lower bound of a nonempty set of real numbers is less than or equal
       to its infimum.  (Contributed by Jeff Hankins, 1-Sep-2013.)  (Revised by
       Mario Carneiro, 6-Sep-2014.) $)
    infmrgelb $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                        E. x e. RR A. y e. A x <_ y ) /\ B e. RR ) ->
                      ( B <_ sup ( A , RR , `' < ) <-> A. z e. A B <_ z ) ) $=
      ( vw cr cv cle wbr wral wrex wcel wa clt wn wb adantr vex brcnv ccnv csup
      wss c0 wne w3a wi infm3 notbii ralbii rexbii imbi12i anbi12i sylibr simpr
      simpl1 wor ltso cnvso suplub2 syl3anc notbid ralnex syl6bbr infmrcl lenlt
      mpbi brcnvg bitr4d syl2anc sselda ralbidva 3bitr4d breq2 cbvralv syl6bb )
      DGUCZDUDUEZAHZBHZIJBDKAGLZUFZEGMZNZEDGOUAZUBZIJZEFHZIJZFDKZECHZIJZCDKWDEW
      FWEJZPZEWHWEJZPZFDKZWGWJWDWNWOFDLZPWQWDWMWRWDVSVTWEJZPZBDKZVTVSWEJZVTWHWE
      JZFDLZUGZBGKZNZAGLZVQWCWMWRQWDVTVSOJZPZBDKZVSVTOJZWHVTOJZFDLZUGZBGKZNZAGL
      ZXHWBXRWCABFDUHRXGXQAGXAXKXFXPWTXJBDWSXIVSVTOASZBSZTUIUJXEXOBGXBXLXDXNVTV
      SOXTXSTXCXMFDVTWHOXTFSTUKULUJUMUKUNVQVRWAWCUPZWBWCUOZABFGDEWEGOUQGWEUQURG
      OUSVGUTVAVBWOFDVCVDWDWCWFGMZWGWNQYBWBYCWCABDVERWCYCNZWGWFEOJZPWNEWFVFYDWM
      YEEWFGGOVHVBVIVJWDWIWPFDWDWHDMZNWCWHGMZWIWPQWDWCYFYBRWDDGWHYAVKWCYGNZWIWH
      EOJZPWPEWHVFYHWOYIEWHGGOVHVBVIVJVLVMWIWLFCDWHWKEIVNVOVP $.
      $( [6-Sep-2014] $) $( [1-Sep-2013] $)
  $}

  ${
    $d x y z B $.
    $( If a nonempty set of real numbers has a lower bound, its infimum is less
       than or equal to any of its elements.  (Contributed by Jeff Hankins,
       15-Sep-2013.) $)
    infmrlb $p |- ( ( B C_ RR /\ E. x e. RR A. y e. B x <_ y /\ A e. B ) ->
                      sup ( B , RR , `' < ) <_ A ) $=
      ( vz cr cv cle wbr wral wrex wcel clt wn wi wa syl3anc vex brcnv ralbii
      wss w3a ccnv csup c0 wne simp1 3ad2ant3 simp2 infm3 notbii rexbii imbi12i
      ne0i anbi12i sylibr simp3 wor ltso cnvso mpbi supub sylc wb infmrcl ssel2
      3adant2 lenlt syl2anc brcnvg notbid bitr4d mpbird ) DFUAZAGZBGZHIBDJAFKZC
      DLZUBZDFMUCZUDZCHIZWACVTIZNZVSVOVPVTIZNZBDJZVPVOVTIZVPEGZVTIZEDKZOZBFJZPZ
      AFKZVRWDVSVPVOMIZNZBDJZVOVPMIZWIVPMIZEDKZOZBFJZPZAFKZWOVSVNDUEUFZVQXEVNVQ
      VRUGZVRVNXFVQDCUNUHZVNVQVRUIZABEDUJQWNXDAFWGWRWMXCWFWQBDWEWPVOVPMARZBRZSU
      KTWLXBBFWHWSWKXAVPVOMXKXJSWJWTEDVPWIMXKERSULUMTUOULUPVNVQVRUQZABEFDCVTFMU
      RFVTURUSFMUTVAVBVCVSWBCWAMIZNZWDVSWAFLZCFLZWBXNVDVSVNXFVQXOXGXHXIABDVEQZV
      NVRXPVQDFCVFVGWACVHVIVSWCXMVSXOVRWCXMVDXQXLWACFDMVJVIVKVLVM $.
      $( [15-Sep-2013] $)
  $}

