$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Reciprocals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( ` _i ` times itself is minus 1.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  ixi $p |- ( _i x. _i ) = -u 1 $=
    ( c1 cneg cc0 cmin co ci cmul df-neg caddc ax-i2m1 0cn ax-1cn ax-icn mulcli
    wceq subadd2i mpbir eqtr2i ) ABCADEZFFGEZAHSTOTAIECOJCATKLFFMMNPQR $.
    $( [19-Nov-2011] $) $( [6-May-1999] $)

  $( Lemma for ~ recex . $)
  recextlem1 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + ( _i x. B ) ) x.
      ( A - ( _i x. B ) ) ) = ( ( A x. A ) + ( B x. B ) ) ) $=
    ( cc wcel wa ci cmul caddc cmin cneg wceq ax-icn mulcl adantl syl3anc subdi
    co sylan2 syl anidms simpl mpan subcl adddir mulcom c1 oveq1i mulm1 syl5req
    mul4 mpanl12 eqtrd oveq12d eqtr4d adantr negcl npncan subneg syl2an 3eqtrd
    ixi ) ACDZBCDZEZAFBGQZHQAVEIQZGQZAVFGQZVEVFGQZHQZAAGQZAVEGQZIQZVLBBGQZJZIQZ
    HQZVKVNHQZVDVBVECDZVFCDZVGVJKVBVCUAZVCVSVBFCDZVCVSLFBMUBZNZVCVBVSVTWCAVEUCR
    AVEVFUDOVDVHVMVIVPHVDVBVBVSVHVMKWAWAWDAAVEPOVDVIVEAGQZVEVEGQZIQZVPVDVSVBVSV
    IWGKWDWAWDVEAVEPOVDVLWEVOWFIVCVBVSVLWEKWCAVEUERVCVOWFKZVBVCWHVCVCEZVOFFGQZV
    NGQZWFWIWKUFJZVNGQZVOWJWLVNGVAUGWIVNCDZWMVOKBBMZVNUHSUIWBWBWIWKWFKLLFFBBUJU
    KULTNUMUNUMVDVQVKVOIQZVRVDVKCDZVLCDZVOCDZVQWPKVBWQVCVBWQAAMTZUOVCVBVSWRWCAV
    EMRVCWSVBVCWSWIWNWSWOVNUPSTNVKVLVOUQOVBWQWNWPVRKVCWTVCWNWOTVKVNURUSULUT $.
    $( [11-Mar-2008] $) $( [23-May-2007] $)

  $( Lemma for ~ recex . $)
  recextlem2 $p |- ( ( A e. RR /\ B e. RR /\ ( A + ( _i x. B ) ) =/= 0 ) ->
                        ( ( A x. A ) + ( B x. B ) ) =/= 0 ) $=
    ( cr wcel ci cmul co caddc cc0 wne clt remulcl anidms wa wceq syl6eq sylan2
    wbr anim12i syl2anc readdcl syl2an 3adant3 wo wn oveq2 ax-icn mul01i oveq12
    w3a necon3ai neorian sylibr cle adantr msqgt0 msqge0 an32s addgtge0 anassrs
    00id addgegt0 jaodan 3impa gt0ne0 ) ACDZBCDZAEBFGZHGZIJZUJAAFGZBBFGZHGZCDZI
    VMKRZVMIJVFVGVNVJVFVKCDZVLCDZVNVGVFVPAALMZVGVQBBLMZVKVLUAUBUCVFVGVJVOVJVFVG
    NZAIJZBIJZUDZVOVJAIOZBIOZNZUEWCWFVIIWFVIIIHGZIWEWDVHIOVIWGOWEVHEIFGIBIEFUFE
    UGUHPAIVHIHUIQVAPUKAIBIULUMVTWAVOWBVTWANVPVQNZIVKKRZIVLUNRZNZVOVTWHWAVFVPVG
    VQVRVSSZUOVFWAVGWKVFWANWIVGWJAUPBUQSURVKVLUSTVTWBNWHIVKUNRZIVLKRZNZVOVTWHWB
    WLUOVFVGWBWOVFWMVGWBNWNAUQBUPSUTVKVLVBTVCQVDVMVET $.
    $( [11-Mar-2008] $) $( [23-May-2007] $)

  ${
    $d x y a b A $.
    $( Existence of reciprocal of nonzero complex number.  (Contributed by Eric
       Schmidt, 22-May-2007.) $)
    recex $p |- ( ( A e. CC /\ A =/= 0 ) -> E. x e. CC ( A x. x ) = 1 ) $=
      ( va vb vy cc wcel cc0 wne cv cmul co c1 wceq wrex ci cr wi wa adantr mpd
      caddc ax-cnre recextlem2 3expia remulcl anidms syl2an ax-rrecex recn cmin
      readdcl sylan ax-icn mulcl subcl sylan2 addcl simpr mulass syl3anc oveq1d
      recextlem1 eqtr3d id sylan9eq oveq2 eqeq1d rcla4ev syl2anc exp31 rexlimdv
      mpan syl5 ex syld wb neeq1 adantl oveq1 rexbidv 3imtr4d rexlimivv syl imp
      ) BFGZBHIZBAJZKLZMNZAFOZWFBCJZPDJZKLZUBLZNZDQOCQOWGWKRZCDBUCWPWQCDQQWLQGZ
      WMQGZSZWPWQWTWPSWOHIZWOWHKLZMNZAFOZWGWKWTXAXDRWPWTXAWLWLKLZWMWMKLZUBLZHIZ
      XDWRWSXAXHWLWMUDUEWTXHXDWTXHSXGEJZKLZMNZEQOZXDWTXGQGZXHXLWRXEQGZXFQGZXMWS
      WRXNWLWLUFUGWSXOWMWMUFUGXEXFULUHEXGUIUMWTXLXDRZXHWRWLFGZWMFGZXPWSWLUJWMUJ
      XQXRSZXKXDEQXIQGXIFGZXSXKXDRXIUJXSXTXKXDXSXTSZXKSWLWNUKLZXIKLZFGZWOYCKLZM
      NZXDYAYDXKXSYBFGZXTYDXRXQWNFGZYGPFGXRYHUNPWMUOVMZWLWNUPUQZYBXIUOUMTYAXKYE
      XJMYAWOYBKLZXIKLZYEXJYAWOFGZYGXTYLYENXSYMXTXRXQYHYMYIWLWNURUQTXSYGXTYJTXS
      XTUSWOYBXIUTVAYAYKXGXIKXSYKXGNXTWLWMVCTVBVDXKVEVFXCYFAYCFWHYCNXBYEMWHYCWO
      KVGVHVIVJVKVNVLUHTUAVOVPTWPWGXAVQWTBWOHVRVSWPWKXDVQWTWPWJXCAFWPWIXBMBWOWH
      KVTVHWAVSWBVOWCWDWE $.
      $( [11-Mar-2008] $) $( [23-May-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    mulcan.1 $e |- A e. CC $.
    mulcan.2 $e |- B e. CC $.
    mulcan.3 $e |- C e. CC $.
    mulcan.4 $e |- C =/= 0 $.
    $( Cancellation law for multiplication.  Theorem I.7 of [Apostol] p. 18. $)
    mulcani $p |- ( ( C x. A ) = ( C x. B ) <-> A = B ) $=
      ( vx cmul co wceq c1 cc wi wcel wa oveq2 wb mulass mp3an3 cv wrex cc0 wne
      recex mp2an eqeq12d mpan2 syl5ibr adantr mulcom mpan eqeq1d oveq1 mulid2i
      syl6eq syl6bi imp sylibd rexlimiva ax-mp impbii ) CAIJZCBIJZKZABKZCHUAZIJ
      ZLKZHMUBZVEVFNZCMOZCUCUDVJFGHCUEUFVIVKHMVGMOZVIPVEVGCIJZAIJZVNBIJZKZVFVMV
      EVQNVIVEVQVMVGVCIJZVGVDIJZKZVCVDVGIQVMVLVQVTRFVMVLPVOVRVPVSVMVLAMOVOVRKDV
      GCASTVMVLBMOVPVSKEVGCBSTUGUHUIUJVMVIVQVFRZVMVIVNLKZWAVMVHVNLVLVMVHVNKFCVG
      UKULUMWBVOAVPBWBVOLAIJAVNLAIUNADUOUPWBVPLBIJBVNLBIUNBEUOUPUGUQURUSUTVAABC
      IQVB $.
      $( [24-Aug-2008] $) $( [26-Jan-1995] $)
  $}

  ${
    mulcant2.1 $e |- C =/= 0 $.
    $( Cancellation law for multiplication.  Theorem I.7 of [Apostol] p. 18.
       Illustrates use of ~ keephyp . $)
    mulcant2i $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( C x. A ) = ( C x. B ) <-> A = B ) ) $=
      ( cc wcel cmul co wceq wb cif oveq2 bibi12d oveq1 ax-1cn elimel cc0 neeq1
      c1 wne eqeq1d eqeq1 eqeq2d eqeq12d bibi1d ax-1ne0 keephyp mulcani dedth3h
      eqeq2 ) AEFZBEFZCEFZCAGHZCBGHZIZABIZJCUKASKZGHZUOIZURBIZJUSCULBSKZGHZIZUR
      VBIZJUMCSKZURGHZVFVBGHZIZVEJABCSSSAURIZUPUTUQVAVJUNUSUOAURCGLUAAURBUBMBVB
      IZUTVDVAVEVKUOVCUSBVBCGLUCBVBURUJMCVFIZVDVIVEVLUSVGVCVHCVFURGNCVFVBGNUDUE
      URVBVFASEOPBSEOPCSEOPUMCQTSQTVFQTCSCVFQRSVFQRDUFUGUHUI $.
      $( [25-Aug-2008] $) $( [29-Jan-1995] $)
  $}

  $( Cancellation law for multiplication (full theorem form).  Theorem I.7 of
     [Apostol] p. 18.  Illustrates use of ~ dedth and ~ elimne0 . $)
  mulcan $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
                 ( ( C x. A ) = ( C x. B ) <-> A = B ) ) $=
    ( cc wcel cc0 wne wa cmul co wceq wb w3a wi cif eleq1 3anbi3d oveq1 eqeq12d
    c1 bibi1d imbi12d elimne0 mulcant2i dedth 3expd com4l imp4a 3imp ) ADEZBDEZ
    CDEZCFGZHCAIJZCBIJZKZABKZLZUJUKULUMURUMUJUKULURUMUJUKULURUMUJUKULMZURNUJUKU
    MCTOZDEZMZUTAIJZUTBIJZKZUQLZNCTCUTKZUSVBURVFVGULVAUJUKCUTDPQVGUPVEUQVGUNVCU
    OVDCUTAIRCUTBIRSUAUBABUTCUCUDUEUFUGUHUI $.
    $( [26-Aug-2008] $) $( [29-Jan-1995] $)

  $( Cancellation law for multiplication. $)
  mulcan2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
                 ( ( A x. C ) = ( B x. C ) <-> A = B ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co wb mulcom 3adant2 3adant1 eqeq12d 3adant3r
    wceq mulcan bitrd ) ADEZBDEZCDEZCFGZHIACJKZBCJKZRZCAJKZCBJKZRZABRUAUBUCUGUJ
    LUDUAUBUCIUEUHUFUIUAUCUEUHRUBACMNUBUCUFUIRUABCMOPQABCST $.
    $( [26-Aug-2008] $) $( [21-Jan-2005] $)

  ${
    mul0or.1 $e |- A e. CC $.
    ${
      mul0or.2 $e |- B e. CC $.
      $( If a product is zero, one of its factors must be zero.  Theorem I.11
         of [Apostol] p. 18. $)
      mul0ori $p |- ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) $=
        ( cmul co cc0 wceq wo wn wne wi df-ne mul01i eqeq2i cc wcel wb syl6eq
        wa 0cn mulcan mp3an12 mpan syl5bbr biimpd sylbir com12 orrd oveq1 oveq2
        mul02i jaoi impbii ) ABEFZGHZAGHZBGHZIUPUQURUQJZUPURUSAGKZUPURLAGMUTUPU
        RUPUOAGEFZHZUTURVAGUOACNZOAPQZUTVBURRZCBPQGPQVDUTTVEDUABGAUBUCUDUEUFUGU
        HUIUQUPURUQUOGBEFGAGBEUJBDULSURUOVAGBGAEUKVCSUMUN $.
        $( [7-Oct-1999] $)
    $}

    $( A number is zero iff its square is zero (where square is represented
       using multiplication). $)
    msq0i $p |- ( ( A x. A ) = 0 <-> A = 0 ) $=
      ( cmul co cc0 wceq wo mul0ori oridm bitri ) AACDEFAEFZKGKAABBHKIJ $.
      $( [28-Jul-1999] $)
  $}

  $( If a product is zero, one of its factors must be zero.  Theorem I.11 of
     [Apostol] p. 18. $)
  mul0or $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) ) $=
    ( cc wcel cmul co cc0 wceq wo wb cif oveq1 eqeq1d eqeq1 orbi1d oveq2 orbi2d
    bibi12d 0cn elimel mul0ori dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZIZJUCAGKZBEFZG
    HZUJGHZUHIZJUJUDBGKZEFZGHZUMUOGHZIZJABGGAUJHZUFULUIUNUTUEUKGAUJBELMUTUGUMUH
    AUJGNORBUOHZULUQUNUSVAUKUPGBUOUJEPMVAUHURUMBUOGNQRUJUOAGCSTBGCSTUAUB $.
    $( [9-Oct-1999] $)

  $( The product of two nonzero numbers is nonzero.  (The proof was shortened
     by Andrew Salmon, 19-Nov-2011.) $)
  mulne0b $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A =/= 0 /\ B =/= 0 ) <->
               ( A x. B ) =/= 0 ) ) $=
    ( cc wcel wa cmul co cc0 wne wceq wo wn mul0or necon3abid neanior syl6rbbr
    ) ACDBCDEZABFGZHIAHJBHJKZLAHIBHIEQSRHABMNAHBHOP $.
    $( [19-Nov-2011] $) $( [1-Aug-2004] $)

  $( The product of two nonzero numbers is nonzero. $)
  mulne0 $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
               ( A x. B ) =/= 0 ) $=
    ( cc wcel cc0 wne cmul co wa mulne0b biimpa an4s ) ACDZBCDZAEFZBEFZABGHEFZM
    NIOPIQABJKL $.
    $( [30-Dec-2007] $)

  ${
    muln0.1 $e |- A e. CC $.
    muln0.2 $e |- B e. CC $.
    muln0.3 $e |- A =/= 0 $.
    muln0.4 $e |- B =/= 0 $.
    $( The product of two nonzero numbers is nonzero. $)
    mulne0i $p |- ( A x. B ) =/= 0 $=
      ( cc wcel cc0 wne cmul co mulne0 mp4an ) AGHAIJBGHBIJABKLIJCEDFABMN $.
      $( [15-Feb-1995] $)
  $}

  $( Property of numbers whose product equals their sum.  Equation 5 of
     [Kreyszig] p. 12. $)
  muleqadd $p |- ( ( A e. CC /\ B e. CC ) ->
             ( ( A x. B ) = ( A + B ) <-> ( ( A - 1 ) x. ( B - 1 ) ) = 1 ) ) $=
    ( cc wcel wa c1 cmin co cmul wceq caddc ax-1cn mulsub mpanr2 mpanl2 mulid1i
    cc0 mulid1 syl2anc wb oveq2i a1i oveqan12d mulcl addcl addsub mp3an2 3eqtrd
    oveq12d eqeq1d addid2i eqeq2i subcl 0cn addcan2 mp3an23 syl syl5rbbr subeq0
    3bitr2rd ) ACDZBCDZEZAFGHBFGHIHZFJABIHZABKHZGHZFKHZFJZVGQJZVEVFJZVCVDVHFVCV
    DVEFFIHZKHZAFIHZBFIHZKHZGHZVEFKHZVFGHZVHVAFCDZVBVDVQJZLVAVTEVBVTWALAFBFMNOV
    CVMVRVPVFGVMVRJVCVLFVEKFLPUAUBVAVBVNAVOBKARBRUCUIVCVECDZVFCDZVSVHJZABUDZABU
    EZWBVTWCWDLVEFVFUFUGSUHUJVIVHQFKHZJZVCVJWGFVHFLUKULVCVGCDZWHVJTZVCWBWCWIWEW
    FVEVFUMSWIQCDVTWJUNLVGQFUOUPUQURVCWBWCVJVKTWEWFVEVFUSSUT $.
    $( [13-Nov-2006] $)

  ${
    $d x y A $.  $d x y B $.
    $( Existential uniqueness of reciprocals.  Theorem I.8 of [Apostol] p. 18.
       (Revised by Mario Carneiro, 17-Feb-2014.) $)
    receu $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                  E! x e. CC ( B x. x ) = A ) $=
      ( vy cc wcel w3a cv cmul co wceq wrex wa wi wral c1 3adant1 syl2anc oveq2
      eqeq1d cc0 wne wreu recex simprl simpll mulcl oveq1 simplr mulass syl3anc
      ad2antll mulid2 syl 3eqtr3d rcla4ev exp32 3adant3 mpd eqtr3 mulcan syl5ib
      rexlimdv 3expa expcom ralrimivv reu4 sylanbrc ) BEFZCEFZCUAUBZGZCAHZIJZBK
      ZAELZVOCDHZIJZBKZMZVMVQKZNZDEOAEOVOAEUCVLVRPKZDELZVPVJVKWDVIDCUDQVIVJWDVP
      NVKVIVJMZWCVPDEWEVQEFZWCVPWEWFWCMZMZVQBIJZEFZCWIIJZBKZVPWHWFVIWJWEWFWCUEZ
      VIVJWGUFZVQBUGRWHVRBIJZPBIJZWKBWCWOWPKWEWFVRPBIUHULWHVJWFVIWOWKKVIVJWGUIW
      MWNCVQBUJUKWHVIWPBKWNBUMUNUOVOWLAWIEVMWIKVNWKBVMWICISTUPRUQVCURUSVLWBADEE
      VJVKVMEFZWFMZWBNVIWRVJVKMZWBWQWFWSWBVTVNVRKWQWFWSGWAVNVRBUTVMVQCVAVBVDVEQ
      VFVOVSADEWAVNVRBVMVQCISTVGVH $.
      $( [17-Feb-2014] $) $( [29-Jan-1995] $)
  $}

  ${
    $d x y $.
    $( Multiplication maps nonzero complex numbers to nonzero complex numbers.
       (Contributed by Steve Rodriguez, 23-Feb-2007.) $)
    mulnzcnopr $p |- ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) :
                             ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) -->
                             ( CC \ { 0 } ) $=
      ( vx vy cc cc0 csn cdif cxp cmul wf wfn cv co wcel ffnov wss wa mp2an wne
      wral eldifsn cres ax-mulopr mpbi simpli difss xpss12 ovres mulcl ad2ant2r
      fnssres mulne0 jca syl2anb sylibr eqeltrd rgen2a mpbir2an ) CDEZFZUSGZUSH
      UTUAZIVAUTJZAKZBKZVALZUSMZBUSSAUSSABUSUSUSVANHCCGZJZUTVGOZVBVHVCVDHLZCMZB
      CSACSZVGCHIVHVLPUBABCCCHNUCUDUSCOZVMVICURUEZVNUSCUSCUFQVGUTHUJQVFABUSVCUS
      MZVDUSMZPZVEVJUSVCVDUSUSHUGVQVKVJDRZPZVJUSMVOVCCMZVCDRZPZVDCMZVDDRZPZVSVP
      VCCDTVDCDTWBWEPVKVRVTWCVKWAWDVCVDUHUIVCVDUKULUMVJCDTUNUOUPUQ $.
      $( [17-Feb-2007] $)
  $}


