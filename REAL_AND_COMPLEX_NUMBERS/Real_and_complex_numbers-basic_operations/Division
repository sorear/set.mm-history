$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Division
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Define division.  Theorem ~ divmuli relates it to multiplication, and
       ~ divcli and ~ redivcli prove its closure laws. $)
    df-div $a |- / = ( x e. CC , y e. ( CC \ { 0 } ) |->
                   ( iota_ z e. CC ( y x. z ) = x ) ) $.
    $(
    Future simpler alternative?
    df-div $a |- / = { <. <. x , y >. , z >. |
                ( ( y e. ( CC \ { 0 } ) /\ z e. CC ) /\  x = ( y x. z ) ) } $.
    $)
  $}

  ${
    $d x y z $.
    $( You can't divide by zero, because division explicitly excludes zero from
       the domain of the function.  Thus, by the definition of function value,
       it evaluates to the empty set.  (This theorem is for information only
       and normally is not referenced by other proofs.  To be meaningful, it
       assumes that ` (/) ` is not a complex number, which depends on the
       particular complex number construction that is used.)  (Contributed by
       Mario Carneiro, 1-Apr-2014.) $)
    1div0 $p |- ( 1 / 0 ) = (/) $=
      ( vx vy vz cdiv cdm cc cc0 csn cdif wceq wcel c1 wa wn co c0 cv cmul crio
      cxp df-div riotaex dmmpt2 0cn eqid eldifsn simprbi adantl necon2bi ndmovg
      wne ax-mp mp3an ) DEFFGHIZTJGFKZLFKZGUNKZMZNZLGDOPJABFUNBQCQROAQJZCFSDABC
      UAUTCFUBUCUDGGJUSGUEURGGUQGGUKZUPUQUOVAGFGUFUGUHUIULLGFFUNDUJUM $.
      $( [2-Apr-2014] $)
  $}

  ${
    $d x y z A $.  $d x y B $.  $d x y C $.
    $( Value of division: the (unique) element ` x ` such that
       ` ( B x. x ) = A ` .  This is meaningful only when ` B ` is nonzero. $)
    divval $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                   ( A / B ) = ( iota_ x e. CC ( B x. x ) = A ) ) $=
      ( vz vy cc wcel cc0 wne cdiv co cmul wceq crio csn cdif eldifsn riotabidv
      cv wa eqeq2 oveq1 eqeq1d df-div riotaex ovmpt2 sylan2br 3impb ) BFGZCFGZC
      HIZBCJKCASZLKZBMZAFNZMZUJUKTUICFHOPZGUPCFHQDEBCFUQESZULLKZDSZMZAFNUOJUSBM
      ZAFNUTBMVAVBAFUTBUSUARURCMZVBUNAFVCUSUMBURCULLUBUCRDEAUDUNAFUEUFUGUH $.
      $( [17-Feb-2014] $) $( [8-May-1999] $)

    $( Relationship between division and multiplication.  (Revised by Mario
       Carneiro, 17-Feb-2014.) $)
    divmul $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
                 -> ( ( A / C ) = B <-> ( C x. B ) = A ) ) $=
      ( vx vy cc wcel cc0 wne wa cdiv co wceq cv cmul crio 3expb 3adant2 eqeq1d
      w3a divval wreu wb simp2 receu ax-17 a17d oveq2 riota2f syl2anc bitr4d )
      AFGZBFGZCFGZCHIZJZTZACKLZBMCDNZOLZAMZDFPZBMZCBOLZAMZUQURVBBULUPURVBMZUMUL
      UNUOVFDACUAQRSUQUMVADFUBZVEVCUCULUMUPUDULUPVGUMULUNUOVGDACUEQRVAVEDEFBENB
      GDUFUMVEDUGUSBMUTVDAUSBCOUHSUIUJUK $.
      $( [17-Feb-2014] $) $( [2-Aug-2004] $)
  $}

  ${
    divmulz.1 $e |- A e. CC $.
    divmulz.2 $e |- B e. CC $.
    divmulz.3 $e |- C e. CC $.
    $( Relationship between division and multiplication. $)
    divmulzi $p |- ( B =/= 0 -> ( ( A / B ) = C <-> ( B x. C ) = A ) ) $=
      ( cc wcel cc0 wne cdiv co wceq cmul wb wa divmul mp3an12 mpan ) BGHZBIJZA
      BKLCMBCNLAMOZEAGHCGHTUAPUBDFACBQRS $.
      $( [17-Feb-2014] $) $( [8-May-1999] $)
  $}

  ${
    divmul.1 $e |- A e. CC $.
    divmul.2 $e |- B e. CC $.
    divmul.3 $e |- C e. CC $.
    divmul.4 $e |- B =/= 0 $.
    $( Relationship between division and multiplication. $)
    divmuli $p |- ( ( A / B ) = C <-> ( B x. C ) = A ) $=
      ( cc0 wne cdiv co wceq cmul wb divmulzi ax-mp ) BHIABJKCLBCMKALNGABCDEFOP
      $.
      $( [17-Feb-2014] $) $( [2-Feb-1995] $)
  $}

  $( Relationship between division and multiplication. $)
  divmul2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
              -> ( ( A / C ) = B <-> A = ( C x. B ) ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co wceq cmul divmul eqcom syl6bb ) ADEBDECDEC
    FGHIACJKBLCBMKZALAQLABCNQAOP $.
    $( [16-Nov-2008] $) $( [7-Feb-2006] $)

  $( Relationship between division and multiplication. $)
  divmul3 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
              -> ( ( A / C ) = B <-> A = ( B x. C ) ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co wceq divmul2 mulcom adantrr 3adant1 eqeq2d
    cmul bitr4d ) ADEZBDEZCDEZCFGZHZIZACJKBLACBRKZLABCRKZLABCMUEUGUFAUAUDUGUFLZ
    TUAUBUHUCBCNOPQS $.
    $( [27-Nov-2008] $) $( [13-Feb-2006] $)

  ${
    $d x A $.  $d x B $.
    $( Closure law for division.  (The proof was shortened by Mario Carneiro,
       17-Feb-2014.) $)
    divcl $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) -> ( A / B ) e. CC ) $=
      ( vx cc wcel cc0 wne w3a cdiv co cmul wceq crio divval wreu receu riotacl
      cv syl eqeltrd ) ADEBDEBFGHZABIJBCRKJALZCDMZDCABNUAUBCDOUCDECABPUBCDQST
      $.
      $( [17-Feb-2014] $) $( [21-Jul-2001] $)

    divclz.1 $e |- A e. CC $.
    divclz.2 $e |- B e. CC $.
    $( Closure law for division. $)
    divclzi $p |- ( B =/= 0 -> ( A / B ) e. CC ) $=
      ( cc wcel cc0 wne cdiv co divcl mp3an12 ) AEFBEFBGHABIJEFCDABKL $.
      $( [17-Feb-2014] $) $( [7-May-1999] $)
  $}

  ${
    divcl.1 $e |- A e. CC $.
    divcl.2 $e |- B e. CC $.
    divcl.3 $e |- B =/= 0 $.
    $( Closure law for division. $)
    divcli $p |- ( A / B ) e. CC $=
      ( cc0 wne cdiv co cc wcel divclzi ax-mp ) BFGABHIJKEABCDLM $.
      $( [17-Feb-2014] $) $( [2-Feb-1995] $)
  $}

  ${
    reccl.1 $e |- A e. CC $.
    reccl.2 $e |- A =/= 0 $.
    $( Closure law for reciprocal. $)
    reccli $p |- ( 1 / A ) e. CC $=
      ( c1 ax-1cn divcli ) DAEBCF $.
      $( [30-Apr-2005] $)
  $}

  ${
    recclz.1 $e |- A e. CC $.
    $( Closure law for reciprocal. $)
    recclzi $p |- ( A =/= 0 -> ( 1 / A ) e. CC ) $=
      ( c1 ax-1cn divclzi ) CADBE $.
      $( [30-Apr-2005] $)
  $}

  $( Closure law for reciprocal. $)
  reccl $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / A ) e. CC ) $=
    ( c1 cc wcel cc0 wne cdiv co ax-1cn divcl mp3an1 ) BCDACDAEFBAGHCDIBAJK $.
    $( [30-Apr-2005] $)

  ${
    divcan.1 $e |- A e. CC $.
    divcan.2 $e |- B e. CC $.
    ${
      divcan.3 $e |- B =/= 0 $.
      $( A cancellation law for division. $)
      divcan2i $p |- ( B x. ( A / B ) ) = A $=
        ( cdiv co wceq cmul eqid divcli divmuli mpbi ) ABFGZNHBNIGAHNJABNCDABCD
        EKELM $.
        $( [18-Aug-2008] $) $( [9-Feb-1995] $)

      $( A cancellation law for division. $)
      divcan1i $p |- ( ( A / B ) x. B ) = A $=
        ( cdiv co cmul divcli mulcomi divcan2i eqtri ) ABFGZBHGBMHGAMBABCDEIDJA
        BCDEKL $.
        $( [18-Aug-2008] $) $( [18-May-1999] $)
    $}

    $( A cancellation law for division. $)
    divcan1zi $p |- ( B =/= 0 -> ( ( A / B ) x. B ) = A ) $=
      ( cc0 wne cdiv co cmul wceq c1 cif oveq2 id oveq12d eqeq1d ax-1cn elimne0
      cc keepel divcan1i dedth ) BEFZABGHZBIHZAJAUCBKLZGHZUFIHZAJBKBUFJZUEUHAUI
      UDUGBUFIBUFAGMUINOPAUFCUCBKSDQTBRUAUB $.
      $( [18-Aug-2008] $) $( [2-Oct-1999] $)

    $( A cancellation law for division.  We eliminate the third hypothesis of
       ~ divcan2i using the weak deduction theorem ~ dedth and keep the other
       two.  Because the first hypothesis shares the class variable ` B ` with
       the hypothesis we're eliminating, we need to use ~ keepel in order to
       keep the first hypothesis. $)
    divcan2zi $p |- ( B =/= 0 -> ( B x. ( A / B ) ) = A ) $=
      ( cc0 wne cdiv co cmul wceq c1 cif id oveq2 oveq12d eqeq1d ax-1cn elimne0
      cc keepel divcan2i dedth ) BEFZBABGHZIHZAJUCBKLZAUFGHZIHZAJBKBUFJZUEUHAUI
      BUFUDUGIUIMBUFAGNOPAUFCUCBKSDQTBRUAUB $.
      $( [21-Aug-2008] $) $( [10-Aug-1999] $)
  $}

  $( A cancellation law for division. $)
  divcan1 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 )
        -> ( ( A / B ) x. B ) = A ) $=
    ( cc wcel cc0 wne cdiv co cmul wceq wi oveq1 oveq1d id eqeq12d imbi2d neeq1
    cif 0cn elimel oveq2 oveq12d eqeq1d imbi12d divcan1zi dedth2h 3impia ) ACDZ
    BCDZBEFZABGHZBIHZAJZUHUIUJUMKUJUHAERZBGHZBIHZUNJZKUIBERZEFZUNURGHZURIHZUNJZ
    KABEEAUNJZUMUQUJVCULUPAUNVCUKUOBIAUNBGLMVCNOPBURJZUJUSUQVBBUREQVDUPVAUNVDUO
    UTBURIBURUNGUAVDNUBUCUDUNURAECSTBECSTUEUFUG $.
    $( [18-Aug-2008] $) $( [5-Jun-2004] $)

  $( A cancellation law for division. $)
  divcan2 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                 ( B x. ( A / B ) ) = A ) $=
    ( cc wcel cc0 wne w3a cdiv co cmul simp2 divcl mulcom syl2anc divcan1 eqtrd
    wceq ) ACDZBCDZBEFZGZBABHIZJIZUBBJIZAUASUBCDUCUDQRSTKABLBUBMNABOP $.
    $( [21-Aug-2008] $) $( [3-Feb-2004] $)

  $( The ratio of nonzero numbers is nonzero. $)
  divne0b $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                  ( A =/= 0 <-> ( A / B ) =/= 0 ) ) $=
    ( cc wcel cc0 wne cdiv co wceq cmul wb mul01 eqeq1d eqcom syl6rbbr 3ad2ant2
    w3a wa 0cn divmul mp3an2 3impb bitr4d necon3bid ) ACDZBCDZBEFZQZAEABGHZEUHA
    EIZBEJHZAIZUIEIZUFUEUJULKUGUFULEAIUJUFUKEABLMAENOPUEUFUGUMULKZUEECDUFUGRUNS
    AEBTUAUBUCUD $.
    $( [1-Jun-2005] $) $( [2-Aug-2004] $)

  $( The ratio of nonzero numbers is nonzero. $)
  divne0 $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
                  ( A / B ) =/= 0 ) $=
    ( cc wcel cc0 wne cdiv co wi w3a divne0b biimpd 3exp com23 com24 imp43 ) AC
    DZAEFZBCDZBEFZABGHEFZQTSRUAQSTRUAIZQSTUBQSTJRUAABKLMNOP $.
    $( [28-Dec-2007] $)

  ${
    divneq0.1 $e |- A e. CC $.
    divneq0.2 $e |- B e. CC $.
    divneq0.3 $e |- A =/= 0 $.
    divneq0.4 $e |- B =/= 0 $.
    $( The ratio of nonzero numbers is nonzero. $)
    divne0i $p |- ( A / B ) =/= 0 $=
      ( cc wcel cc0 wne cdiv co w3a divne0b mpbii mp3an ) AGHZBGHZBIJZABKLIJZCD
      FQRSMAIJTEABNOP $.
      $( [9-Feb-1995] $)
  $}

  ${
    recneq0z.1 $e |- A e. CC $.
    $( The reciprocal of a nonzero number is nonzero. $)
    recne0zi $p |- ( A =/= 0 -> ( 1 / A ) =/= 0 ) $=
      ( cc0 wne c1 cdiv co cif wceq oveq2 neeq1d ax-1cn ax-1ne0 elimne0 divne0i
      cc keepel dedth ) ACDZEAFGZCDESAEHZFGZCDAEAUAITUBCAUAEFJKEUALSAEPBLQMANOR
      $.
      $( [14-May-1999] $)
  $}

  $( The reciprocal of a nonzero number is nonzero. $)
  recne0 $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / A ) =/= 0 ) $=
    ( cc wcel cc0 wne c1 cdiv co cif wceq neeq1 oveq2 neeq1d imbi12d 0cn elimel
    wi recne0zi dedth imp ) ABCZADEZFAGHZDEZUAUBUDQUAADIZDEZFUEGHZDEZQADAUEJZUB
    UFUDUHAUEDKUIUCUGDAUEFGLMNUEADBOPRST $.
    $( [9-Feb-2006] $)

  ${
    recid.1 $e |- A e. CC $.
    recid.2 $e |- A =/= 0 $.
    $( Multiplication of a number and its reciprocal. $)
    recidi $p |- ( A x. ( 1 / A ) ) = 1 $=
      ( c1 ax-1cn divcan2i ) DAEBCF $.
      $( [9-Feb-1995] $)
  $}

  ${
    recidz.1 $e |- A e. CC $.
    $( Multiplication of a number and its reciprocal. $)
    recidzi $p |- ( A =/= 0 -> ( A x. ( 1 / A ) ) = 1 ) $=
      ( c1 ax-1cn divcan2zi ) CADBE $.
      $( [14-May-1999] $)
  $}

  $( Multiplication of a number and its reciprocal. $)
  recid $p |- ( ( A e. CC /\ A =/= 0 ) -> ( A x. ( 1 / A ) ) = 1 ) $=
    ( cc wcel cc0 wne c1 cdiv co cmul wceq wi cif neeq1 id oveq2 oveq12d eqeq1d
    imbi12d 0cn elimel recidzi dedth imp ) ABCZADEZAFAGHZIHZFJZUDUEUHKUDADLZDEZ
    UIFUIGHZIHZFJZKADAUIJZUEUJUHUMAUIDMUNUGULFUNAUIUFUKIUNNAUIFGOPQRUIADBSTUAUB
    UC $.
    $( [25-Oct-1999] $)

  $( Multiplication of a number and its reciprocal. $)
  recid2 $p |- ( ( A e. CC /\ A =/= 0 ) -> ( ( 1 / A ) x. A ) = 1 ) $=
    ( cc wcel cc0 wne wa c1 cdiv co cmul reccl simpl mulcom syl2anc recid eqtrd
    wceq ) ABCZADEZFZGAHIZAJIZAUAJIZGTUABCRUBUCQAKRSLUAAMNAOP $.
    $( [22-Jun-2006] $)

  ${
    divrec.1 $e |- A e. CC $.
    divrec.2 $e |- B e. CC $.
    ${
      divrec.3 $e |- B =/= 0 $.
      $( Relationship between division and reciprocal.  Theorem I.9 of
         [Apostol] p. 18. $)
      divreci $p |- ( A / B ) = ( A x. ( 1 / B ) ) $=
        ( cdiv co c1 cmul reccli mulcli mulcomi mulassi ax-1cn divcan1i mulid1i
        wceq oveq2i eqtri 3eqtri divmuli mpbir ) ABFGAHBFGZIGZQBUDIGZAQUEUDBIGA
        UCBIGZIGZABUDDAUCCBDEJZKZLAUCBCUHDMUGAHIGAUFHAIHBNDEORACPSTABUDCDUIEUAU
        B $.
        $( [9-Feb-1995] $)
    $}

    $( Relationship between division and reciprocal.  Theorem I.9 of [Apostol]
       p. 18. $)
    divreczi $p |- ( B =/= 0 -> ( A / B ) = ( A x. ( 1 / B ) ) ) $=
      ( cc0 wne cdiv co c1 cmul wceq cif oveq2 oveq2d eqeq12d cc ax-1cn elimne0
      keepel divreci dedth ) BEFZABGHZAIBGHZJHZKAUBBILZGHZAIUFGHZJHZKBIBUFKZUCU
      GUEUIBUFAGMUJUDUHAJBUFIGMNOAUFCUBBIPDQSBRTUA $.
      $( [11-Oct-1999] $)
  $}

  $( Relationship between division and reciprocal.  Theorem I.9 of [Apostol]
     p. 18. $)
  divrec $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                ( A / B ) = ( A x. ( 1 / B ) ) ) $=
    ( cc wcel cc0 wne cdiv co c1 cmul wceq cif oveq1 eqeq12d imbi2d neeq1 oveq2
    wi 0cn elimel oveq2d imbi12d divreczi dedth2h 3impia ) ACDZBCDZBEFZABGHZAIB
    GHZJHZKZUFUGUHULRUHUFAELZBGHZUMUJJHZKZRUGBELZEFZUMUQGHZUMIUQGHZJHZKZRABEEAU
    MKZULUPUHVCUIUNUKUOAUMBGMAUMUJJMNOBUQKZUHURUPVBBUQEPVDUNUSUOVABUQUMGQVDUJUT
    UMJBUQIGQUANUBUMUQAECSTBECSTUCUDUE $.
    $( [4-May-2005] $) $( [2-Aug-2004] $)

  $( Relationship between division and reciprocal. $)
  divrec2 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                ( A / B ) = ( ( 1 / B ) x. A ) ) $=
    ( cc wcel cc0 wne w3a cdiv co cmul divrec wceq simp1 3adant1 mulcom syl2anc
    c1 reccl eqtrd ) ACDZBCDZBEFZGZABHIAQBHIZJIZUDAJIZABKUCTUDCDZUEUFLTUAUBMUAU
    BUGTBRNAUDOPS $.
    $( [7-Feb-2006] $)

  $( An associative law for division. $)
  divass $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
                ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
    ( cc wcel cc0 wne wa cmul co c1 cdiv wceq reccl mulass syl3an3 mulcl divrec
    w3a syl3anc 3adant3 simp3l simp3r simp2 oveq2d 3eqtr4d ) ADEZBDEZCDEZCFGZHZ
    SZABIJZKCLJZIJZABUNIJZIJZUMCLJZABCLJZIJUKUGUHUNDEUOUQMCNABUNOPULUMDEZUIUJUR
    UOMUGUHUTUKABQUAUGUHUIUJUBZUGUHUIUJUCZUMCRTULUSUPAIULUHUIUJUSUPMUGUHUKUDVAV
    BBCRTUEUF $.
    $( [12-Oct-2008] $) $( [2-Aug-2004] $)

  $( A commutative/associative law for division. $)
  div23 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
            -> ( ( A x. B ) / C ) = ( ( A / C ) x. B ) ) $=
    ( cc wcel cc0 wne w3a cmul cdiv wceq ax-mulcom oveq1d 3adant3 divass 3com12
    wa co simp2 divcl 3expb 3adant2 mulcom syl2anc 3eqtrd ) ADEZBDEZCDEZCFGZQZH
    ZABIRZCJRZBAIRZCJRZBACJRZIRZUPBIRZUFUGUMUOKUJUFUGQULUNCJABLMNUGUFUJUOUQKBAC
    OPUKUGUPDEZUQURKUFUGUJSUFUJUSUGUFUHUIUSACTUAUBBUPUCUDUE $.
    $( [27-Dec-2008] $) $( [2-Aug-2004] $)

  $( A commutative/associative law for division. $)
  div13 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\ C e. CC )
            -> ( ( A / B ) x. C ) = ( ( C / B ) x. A ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co cdiv ax-mulcom oveq1d 3adant2 div23 3com23
    wceq 3coml 3eqtr3d ) ADEZBDEBFGHZCDEZIACJKZBLKZCAJKZBLKZABLKCJKZCBLKAJKZUAU
    CUEUGRUBUAUCHUDUFBLACMNOUAUCUBUEUHRACBPQUCUAUBUGUIRCABPST $.
    $( [22-Apr-2005] $)

  $( A commutative/associative law for division. $)
  div12 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
            -> ( A x. ( B / C ) ) = ( B x. ( A / C ) ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co cmul divcl 3expb mulcom sylan2 3impb div13
    wceq 3comr sylan 3impa 3com23 3eqtrd ) ADEZBDEZCDEZCFGZHZIABCJKZLKZUJALKZAC
    JKZBLKZBUMLKZUEUFUIUKULSZUFUIHUEUJDEZUPUFUGUHUQBCMNAUJOPQUFUIUEULUNSBCARTUE
    UIUFUNUOSZUEUIUFURUEUIHUMDEZUFURUEUGUHUSACMNUMBOUAUBUCUD $.
    $( [11-Feb-2009] $) $( [30-Apr-2005] $)

  ${
    divass.1 $e |- A e. CC $.
    divass.2 $e |- B e. CC $.
    divass.3 $e |- C e. CC $.
    $( An associative law for division. $)
    divasszi $p |- ( C =/= 0 -> ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
      ( cc wcel cc0 wne cmul co cdiv wceq wa divass mp3an12 mpan ) CGHZCIJZABKL
      CMLABCMLKLNZFAGHBGHSTOUADEABCPQR $.
      $( [12-Aug-1999] $)

    ${
      divass.4 $e |- C =/= 0 $.
      $( An associative law for division. $)
      divassi $p |- ( ( A x. B ) / C ) = ( A x. ( B / C ) ) $=
        ( cc0 wne cmul co cdiv wceq divasszi ax-mp ) CHIABJKCLKABCLKJKMGABCDEFN
        O $.
        $( [15-Feb-1995] $)

      $( Distribution of division over addition. $)
      divdiri $p |- ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) $=
        ( caddc co c1 cdiv cmul reccli adddiri addcli divreci oveq12i 3eqtr4i )
        ABHIZJCKIZLIATLIZBTLIZHISCKIACKIZBCKIZHIABTDECFGMNSCABDEOFGPUCUAUDUBHAC
        DFGPBCEFGPQR $.
        $( [16-Feb-1995] $)

      $( A commutative/associative law for division. $)
      div23i $p |- ( ( A x. B ) / C ) = ( ( A / C ) x. B ) $=
        ( cmul co cdiv mulcomi oveq1i divassi divcli 3eqtri ) ABHIZCJIBAHIZCJIB
        ACJIZHIRBHIPQCJABDEKLBACEDFGMBREACDFGNKO $.
        $( [3-Sep-1999] $)
    $}

    $( Distribution of division over addition. $)
    divdirzi $p |- ( C =/= 0 ->
                    ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) ) $=
      ( cc0 wne caddc co cdiv wceq c1 cif oveq2 oveq12d eqeq12d ax-1cn keepel
      cc elimne0 divdiri dedth ) CGHZABIJZCKJZACKJZBCKJZIJZLUEUDCMNZKJZAUJKJZBU
      JKJZIJZLCMCUJLZUFUKUIUNCUJUEKOUOUGULUHUMICUJAKOCUJBKOPQABUJDEUDCMTFRSCUAU
      BUC $.
      $( [31-Jul-2004] $)
  $}

  $( Distribution of division over addition. $)
  divdir $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
               -> ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) ) $=
    ( cc wcel cc0 wne caddc co cdiv wceq wi cif oveq1 oveq1d eqeq12d imbi2d 0cn
    oveq2 elimel oveq2d neeq1 oveq12d imbi12d divdirzi dedth3h 3exp imp4a 3imp
    wa ) ADEZBDEZCDEZCFGZUJABHIZCJIZACJIZBCJIZHIZKZUKULUMUNUTUKULUMUNUTLZUKULUM
    VAUNUKAFMZBHIZCJIZVBCJIZURHIZKZLUNVBULBFMZHIZCJIZVEVHCJIZHIZKZLUMCFMZFGZVIV
    NJIZVBVNJIZVHVNJIZHIZKZLABCFFFAVBKZUTVGUNWAUPVDUSVFWAUOVCCJAVBBHNOWAUQVEURH
    AVBCJNOPQBVHKZVGVMUNWBVDVJVFVLWBVCVICJBVHVBHSOWBURVKVEHBVHCJNUAPQCVNKZUNVOV
    MVTCVNFUBWCVJVPVLVSCVNVIJSWCVEVQVKVRHCVNVBJSCVNVHJSUCPUDVBVHVNAFDRTBFDRTCFD
    RTUEUFUGUHUI $.
    $( [20-Aug-2008] $) $( [31-Jul-2004] $)

  ${
    divcan3.1 $e |- A e. CC $.
    divcan3.2 $e |- B e. CC $.
    ${
      divcan3.3 $e |- B =/= 0 $.
      $( A cancellation law for division. $)
      divcan3i $p |- ( ( B x. A ) / B ) = A $=
        ( cmul co cdiv divassi divcan2i eqtri ) BAFGBHGBABHGFGABABDCDEIABCDEJK
        $.
        $( [22-Aug-2008] $) $( [16-Feb-1995] $)

      $( A cancellation law for division. $)
      divcan4i $p |- ( ( A x. B ) / B ) = A $=
        ( cmul co cdiv mulcomi oveq1i divcan3i eqtri ) ABFGZBHGBAFGZBHGAMNBHABC
        DIJABCDEKL $.
        $( [22-Aug-2008] $) $( [18-May-1999] $)
    $}

    $( A cancellation law for division.  (Eliminates a hypothesis of ~ divcan3i
       with the weak deduction theorem.) $)
    divcan3zi $p |- ( B =/= 0 -> ( ( B x. A ) / B ) = A ) $=
      ( cc0 wne cmul co cdiv wceq c1 cif oveq1 id oveq12d eqeq1d ax-1cn elimne0
      cc keepel divcan3i dedth ) BEFZBAGHZBIHZAJUCBKLZAGHZUFIHZAJBKBUFJZUEUHAUI
      UDUGBUFIBUFAGMUINOPAUFCUCBKSDQTBRUAUB $.
      $( [23-Aug-2008] $) $( [3-Feb-2004] $)

    $( A cancellation law for division. $)
    divcan4zi $p |- ( B =/= 0 -> ( ( A x. B ) / B ) = A ) $=
      ( cc0 wne cmul co cdiv mulcomi oveq1i divcan3zi syl5eq ) BEFABGHZBIHBAGHZ
      BIHANOBIABCDJKABCDLM $.
      $( [23-Aug-2008] $) $( [12-Oct-1999] $)

  $}

  $( A cancellation law for division. $)
  divcan3 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                 ( ( B x. A ) / B ) = A ) $=
    ( cc wcel cc0 wne cmul co cdiv wceq wi oveq2 oveq1d id eqeq12d imbi2d neeq1
    cif 0cn elimel oveq1 oveq12d eqeq1d imbi12d divcan3zi dedth2h 3impia ) ACDZ
    BCDZBEFZBAGHZBIHZAJZUHUIUJUMKUJBUHAERZGHZBIHZUNJZKUIBERZEFZURUNGHZURIHZUNJZ
    KABEEAUNJZUMUQUJVCULUPAUNVCUKUOBIAUNBGLMVCNOPBURJZUJUSUQVBBUREQVDUPVAUNVDUO
    UTBURIBURUNGUAVDNUBUCUDUNURAECSTBECSTUEUFUG $.
    $( [23-Aug-2008] $) $( [3-Feb-2004] $)

  $( A cancellation law for division. $)
  divcan4 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                 ( ( A x. B ) / B ) = A ) $=
    ( cc wcel cc0 wne w3a cmul co cdiv wceq mulcom 3adant3 oveq1d divcan3 eqtrd
    ) ACDZBCDZBEFZGZABHIZBJIBAHIZBJIATUAUBBJQRUAUBKSABLMNABOP $.
    $( [23-Aug-2008] $) $( [8-Feb-2005] $)

  ${
    div11.1 $e |- A e. CC $.
    div11.2 $e |- B e. CC $.
    div11.3 $e |- C e. CC $.
    div11.4 $e |- C =/= 0 $.
    $( One-to-one relationship for division. $)
    div11i $p |- ( ( A / C ) = ( B / C ) <-> A = B ) $=
      ( cdiv co wceq cmul divcli mulcani divcan2i eqeq12i bitr3i ) ACHIZBCHIZJC
      QKIZCRKIZJABJQRCACDFGLBCEFGLFGMSATBACDFGNBCEFGNOP $.
      $( [20-Aug-2001] $)
  $}

  $( One-to-one relationship for division. $)
  div11 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
               ( ( A / C ) = ( B / C ) <-> A = B ) ) $=
    ( cc wcel cc0 wne wa cdiv co wceq wb c1 cif oveq1 oveq2 ax-1cn elimel eleq1
    bibi12d eqeq1d eqeq1 eqeq2d eqeq2 eqeq12d bibi1d neeq1 pm3.2i simpli simpri
    anbi12d ax-1ne0 elimhyp div11i dedth3h ) ADEZBDEZCDEZCFGZHZACIJZBCIJZKZABKZ
    LUPAMNZCIJZVBKZVEBKZLVFUQBMNZCIJZKZVEVIKZLVEUTCMNZIJZVIVMIJZKZVLLABCMMMAVEK
    ZVCVGVDVHVQVAVFVBAVECIOUAAVEBUBTBVIKZVGVKVHVLVRVBVJVFBVICIOUCBVIVEUDTCVMKZV
    KVPVLVSVFVNVJVOCVMVEIPCVMVIIPUEUFVEVIVMAMDQRBMDQRVMDEZVMFGZUTVTWAHMDEZMFGZH
    CMVSURVTUSWACVMDSCVMFUGUKMVMKWBVTWCWAMVMDSMVMFUGUKWBWCQULUHUMZUIVTWAWDUJUNU
    O $.
    $( [20-Apr-2006] $)

  $( A number divided by itself is one. $)
  divid $p |- ( ( A e. CC /\ A =/= 0 ) -> ( A / A ) = 1 ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul wceq divrec 3anidm12 recid eqtrd ) ABC
    ZADEZFAAGHZAIAGHJHZIPQRSKAALMANO $.
    $( [1-Aug-2004] $)

  $( Division into zero is zero. $)
  div0 $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 0 / A ) = 0 ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul wceq 0cn divrec mp3an1 reccl mul02 syl
    eqtrd ) ABCZADEZFZDAGHZDIAGHZJHZDDBCSTUBUDKLDAMNUAUCBCUDDKAOUCPQR $.
    $( [14-Mar-2005] $)

  $( A ratio is zero iff the numerator is zero. $)
  diveq0 $p |- ( ( A e. CC /\ C e. CC /\ C =/= 0 ) ->
               ( ( A / C ) = 0 <-> A = 0 ) ) $=
    ( cc wcel cc0 wne cdiv co wceq wb wa div0 eqeq2d 3adant1 div11 mp3an2 3impb
    w3a 0cn bitr3d ) ACDZBCDZBEFZRABGHZEBGHZIZUDEIZAEIZUBUCUFUGJUAUBUCKZUEEUDBL
    MNUAUBUCUFUHJZUAECDUIUJSAEBOPQT $.
    $( [20-Apr-2006] $)

  ${
    recrec.1 $e |- A e. CC $.
    recrec.2 $e |- A =/= 0 $.
    $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
       [Apostol] p. 18. $)
    recreci $p |- ( 1 / ( 1 / A ) ) = A $=
      ( c1 cdiv cmul reccli ax-1cn ax-1ne0 divne0i recidi oveq2i oveq1i mulassi
      co mulid2i 3eqtr3i mulid1i ) ADAEOZDSEOZFOZFOZADFOTAUADAFSABCGZDAHBICJZKL
      ASFOZTFODTFOUBTUEDTFABCKMASTBUCSUCUDGZNTUFPQABRQ $.
      $( [9-Feb-1995] $)

    $( A number divided by itself is one. $)
    dividi $p |- ( A / A ) = 1 $=
      ( cdiv co c1 cmul divreci recidi eqtri ) AADEAFADEGEFAABBCHABCIJ $.
      $( [9-Feb-1995] $)

    $( Division into zero is zero. $)
    div0i $p |- ( 0 / A ) = 0 $=
      ( cc wcel cc0 wne cdiv co wceq div0 mp2an ) ADEAFGFAHIFJBCAKL $.
      $( [12-Aug-1999] $)
  $}

  ${
    div1.1 $e |- A e. CC $.
    $( A number divided by 1 is itself. $)
    div1i $p |- ( A / 1 ) = A $=
      ( c1 cdiv co wceq cmul mulid2i ax-1cn ax-1ne0 divmuli mpbir ) ACDEAFCAGEA
      FABHACABIBJKL $.
      $( [9-Jan-2002] $)
  $}

  $( A number divided by 1 is itself. $)
  div1 $p |- ( A e. CC -> ( A / 1 ) = A ) $=
    ( cc wcel c1 cdiv co wceq cif oveq1 id eqeq12d ax-1cn elimel div1i dedth )
    ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [9-Jan-2002] $)

  $( Move negative sign inside of a division. $)
  divneg $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                -u ( A / B ) = ( -u A / B ) ) $=
    ( cc wcel cc0 wne w3a cneg c1 cdiv co cmul wceq wa reccl sylan2 3impb negcl
    mulneg1 divrec syl3an1 negeqd 3eqtr4rd ) ACDZBCDZBEFZGZAHZIBJKZLKZAUILKZHZU
    HBJKZABJKZHUDUEUFUJULMZUEUFNUDUICDUOBOAUISPQUDUHCDUEUFUMUJMARUHBTUAUGUNUKAB
    TUBUC $.
    $( [3-Jun-2005] $) $( [17-Sep-2004] $)

  $( Distribution of division over subtraction. $)
  divsubdir $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
               -> ( ( A - B ) / C ) = ( ( A / C ) - ( B / C ) ) ) $=
    ( cc wcel cc0 wne wa w3a cneg caddc co cdiv cmin negcl negsub 3expb 3adant1
    wceq divcl divdir syl3an2 oveq1d 3adant3 divneg 3adant2 syl2anc 3eqtr3d
    oveq2d eqtr3d ) ADEZBDEZCDEZCFGZHZIZABJZKLZCMLZACMLZUQCMLZKLZABNLZCMLZUTBCM
    LZNLZULUKUQDEUOUSVBSBOAUQCUAUBUKULUSVDSUOUKULHURVCCMABPUCUDUPUTVEJZKLZVBVFU
    PVGVAUTKULUOVGVASZUKULUMUNVIBCUEQRUIUPUTDEZVEDEZVHVFSUKUOVJULUKUMUNVJACTQUF
    ULUOVKUKULUMUNVKBCTQRUTVEPUGUJUH $.
    $( [6-Sep-2008] $) $( [4-Mar-2005] $)

  $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
     [Apostol] p. 18. $)
  recrec $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / ( 1 / A ) ) = A ) $=
    ( cc wcel cc0 wne wa c1 cdiv co wceq cif oveq2 oveq2d eqeq12d eleq1 anbi12d
    id neeq1 ax-1cn ax-1ne0 pm3.2i elimhyp simpli simpri recreci dedth ) ABCZAD
    EZFZGGAHIZHIZAJGGUIAGKZHIZHIZULJAGAULJZUKUNAULUOUJUMGHAULGHLMUOQNULULBCZULD
    EZUIUPUQFGBCZGDEZFAGUOUGUPUHUQAULBOAULDRPGULJURUPUSUQGULBOGULDRPURUSSTUAUBZ
    UCUPUQUTUDUEUF $.
    $( [26-Sep-1999] $)

  ${
    rec11.1 $e |- A e. CC $.
    rec11.2 $e |- B e. CC $.
    ${
      rec11i.3 $e |- A =/= 0 $.
      rec11i.4 $e |- B =/= 0 $.
      $( Reciprocal is one-to-one. $)
      rec11ii $p |- ( ( 1 / A ) = ( 1 / B ) <-> A = B ) $=
        ( c1 cdiv co wceq cmul oveq2 reccli mul32i recidi oveq1i mulid2i 3eqtri
        mulassi oveq2i mulid1i eqeq12i eqcom bitri sylib impbii ) GAHIZGBHIZJZA
        BJZUIABKIZUGKIZUKUHKIZJZUJUGUHUKKLUNBAJUJULBUMAULAUGKIZBKIGBKIBABUGCDAC
        EMNUOGBKACEOPBDQRUMABUHKIZKIAGKIAABUHCDBDFMSUPGAKBDFOTACUARUBBAUCUDUEAB
        GHLUF $.
        $( [16-Sep-1999] $)
    $}

    $( Reciprocal is one-to-one. $)
    rec11i $p |- ( ( A =/= 0 /\ B =/= 0 ) ->
               ( ( 1 / A ) = ( 1 / B ) <-> A = B ) ) $=
      ( cc0 wne c1 cdiv co wceq wb cif oveq2 eqeq1d eqeq1 bibi12d ax-1cn keepel
      cc elimne0 eqeq2d eqeq2 rec11ii dedth2h ) AEFZBEFZGAHIZGBHIZJZABJZKGUEAGL
      ZHIZUHJZUKBJZKULGUFBGLZHIZJZUKUOJZKABGGAUKJZUIUMUJUNUSUGULUHAUKGHMNAUKBOP
      BUOJZUMUQUNURUTUHUPULBUOGHMUABUOUKUBPUKUOUEAGSCQRUFBGSDQRATBTUCUD $.
      $( [16-Sep-1999] $)
  $}

  $( Mutual reciprocals.  (Contributed by Paul Chapman, 18-Oct-2007.) $)
  rec11r $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) )
                  -> ( ( 1 / A ) = B <-> ( 1 / B ) = A ) ) $=
    ( cc wcel cc0 wne wa c1 cdiv co wceq wb ax-1cn divmul mp3an1 ancoms adantrr
    cmul mulcom bitr4d eqeq1d adantlr ) ACDZAEFZGZBCDZBEFZGZGHAIJBKZABRJZHKZHBI
    JAKZUEUFUIUKLZUGUFUEUMHCDZUFUEUMMHBANOPQUCUHULUKLUDUCUHGZULBARJZHKZUKUNUCUH
    ULUQLMHABNOUOUJUPHUCUFUJUPKUGABSQUATUBT $.
    $( [27-Dec-2008] $) $( [18-Oct-2007] $)

  $( Multiplication of two ratios.  Theorem I.14 of [Apostol] p. 18. $)
  divmuldiv $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 )
                    /\ ( D e. CC /\ D =/= 0 ) ) )
               -> ( ( A / C ) x. ( B / D ) ) = ( ( A x. B ) / ( C x. D ) ) ) $=
    ( cc wcel cc0 wne wa cdiv co cmul w3a 3anass divcl syl2an 3adantr1 3adantl1
    wceq simp2 mulcl ax-mulcl ad2ant2r mulne0 divcan3 syl3anc divcan2 oveqan12d
    jca mul4 eqtr3d oveq1d syl2anbr an4s ) AEFZCEFZCGHZIZBEFZDEFZDGHZIZACJKZBDJ
    KZLKZABLKZCDLKZJKZSZUOURIUOUPUQMZUSUTVAMZVIUSVBIUOUPUQNUSUTVANVJVKIZVGVELKZ
    VGJKZVEVHVLVEEFZVGEFZVGGHZVNVESVJVCEFZVDEFZVOVKACOZBDOZVCVDUAPUPUQVKVPUOURU
    TVAVPUSUPUTVPUQVACDUBUCQRUPUQVKVQUOURUTVAVQUSCDUDQRVEVGUEUFVLVMVFVGJVLCVCLK
    ZDVDLKZLKZVMVFVJUPVRIUTVSIWDVMSVKVJUPVRUOUPUQTVTUIVKUTVSUSUTVATWAUICVCDVDUJ
    PVJVKWBAWCBLACUGBDUGUHUKULUKUMUN $.
    $( [28-Dec-2008] $) $( [1-Aug-2004] $)

  $( Cancellation of common factor in a ratio. $)
  divcan5 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\
       ( C e. CC /\ C =/= 0 ) ) -> ( ( C x. A ) / ( C x. B ) ) = ( A / B ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co cmul c1 divid oveq1d 3ad2ant3 simp3l simp1
    wceq simp3 simp2 divmuldiv syl22anc divcl 3expb mulid2 syl 3adant3 3eqtr3d
    ) ADEZBDEZBFGZHZCDEZCFGZHZIZCCJKZABJKZLKZMUSLKZCALKCBLKJKZUSUPUJUTVASUMUPUR
    MUSLCNOPUQUNUJUPUMUTVBSUJUMUNUOQUJUMUPRUJUMUPTUJUMUPUACACBUBUCUJUMVAUSSZUPU
    JUMHUSDEZVCUJUKULVDABUDUEUSUFUGUHUI $.
    $( [9-Jan-2006] $)

  $( Swap the denominators in the product of two ratios. $)
  divmul13 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 )
                    /\ ( D e. CC /\ D =/= 0 ) ) )
                -> ( ( A / C ) x. ( B / D ) ) = ( ( B / C ) x. ( A / D ) ) ) $=
    ( cc wcel wa cc0 wne cmul co cdiv ax-mulcom adantr oveq1d divmuldiv ancom1s
    wceq 3eqtr4d ) AEFZBEFZGZCEFCHIGDEFDHIGGZGZABJKZCDJKZLKBAJKZUFLKZACLKBDLKJK
    BCLKADLKJKZUDUEUGUFLUBUEUGRUCABMNOABCDPUATUCUIUHRBACDPQS $.
    $( [14-Feb-2009] $) $( [3-May-2005] $)

  $( Swap the numerators in the product of two ratios. $)
  divmul24 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 )
                    /\ ( D e. CC /\ D =/= 0 ) ) )
                -> ( ( A / C ) x. ( B / D ) ) = ( ( A / D ) x. ( B / C ) ) ) $=
    ( cc wcel cc0 wne cmul cdiv wceq ax-mulcom ad2ant2r adantl oveq2d divmuldiv
    wa co ancom2s 3eqtr4d ) AEFBEFQZCEFZCGHZQZDEFZDGHZQZQZQZABIRZCDIRZJRUJDCIRZ
    JRZACJRBDJRIRADJRBCJRIRZUIUKULUJJUHUKULKZUAUBUEUOUCUFCDLMNOABCDPUAUGUDUNUMK
    ABDCPST $.
    $( [18-Feb-2009] $) $( [3-May-2005] $)

  $( Addition of two ratios.  Theorem I.13 of [Apostol] p. 18. $)
  divadddiv $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 )
                    /\ ( D e. CC /\ D =/= 0 ) ) ) ->
  ( ( A / C ) + ( B / D ) ) = ( ( ( A x. D ) + ( C x. B ) ) / ( C x. D ) ) ) $=
    ( cc wcel wa cc0 wne cmul caddc cdiv wceq ax-mulcl ad2ant2r adantrl adantrr
    co c1 divid ancoms ad2ant2lr mulne0 jca adantl divdir syl3anc oveq2d simprl
    ad2antll anim2i divmuldiv sylancom w3a divcl 3expb 3eqtr3d adantlr ad2antrl
    mulid1 syl oveq1d pm3.22 mulid2 adantll oveq12d eqtr2d ) AEFZBEFZGZCEFZCHIZ
    GZDEFZDHIZGZGZGZADJRZCBJRZKRCDJRZLRZVSWALRZVTWALRZKRZACLRZBDLRZKRVRVSEFZVTE
    FZWAEFZWAHIZGZWBWEMVJVPWHVMVHVNWHVIVOADNOPVIVMWIVHVPVIVKWIVLVKVIWICBNUAQUBV
    QWLVJVQWJWKVKVNWJVLVOCDNOCDUCUDUEVSVTWAUFUGVRWCWFWDWGKVHVQWCWFMVIVHVQGZWFDD
    LRZJRZWFSJRZWCWFWMWNSWFJVPWNSMVHVMDTUJUHVHVQVHVNGWOWCMVQVNVHVMVNVOUIUKADCDU
    LUMVHVMWPWFMZVPVHVKVLWQVHVKVLUNWFEFWQACUOWFUTVAUPQUQURVIVQWDWGMVHVIVQGZCCLR
    ZWGJRZSWGJRZWDWGWRWSSWGJVMWSSMVIVPCTUSVBVIVQVKVIGZWTWDMVIVMXBVPVIVKXBVLVIVK
    VCQQCBCDULUMVIVPXAWGMZVMVIVNVOXCVIVNVOUNWGEFXCBDUOWGVDVAUPPUQVEVFVG $.
    $( [21-Feb-2009] $) $( [1-Aug-2004] $)

  $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
  divdivdiv $p |- ( ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) ) /\
                  ( ( C e. CC /\ C =/= 0 ) /\ ( D e. CC /\ D =/= 0 ) ) )
                -> ( ( A / B ) / ( C / D ) ) = ( ( A x. D ) / ( B x. C ) ) ) $=
    ( cc wcel cc0 wa cdiv co cmul wceq divcl syl3anc mulcom syl2anc syl22anc c1
    wne mulcl simprrl simprll simprlr simpll simplrl simplr simprl eqtrd oveq2d
    simplrr divmuldiv simprr oveq1d simprrr mulne0 3eqtrd mulass mulid2 3eqtr3d
    divid syl eqtr3d wb ad2ant2lr divne0 adantl divmul syl112anc mpbird ) AEFZB
    EFZBGSZHZHZCEFZCGSZHZDEFZDGSZHZHZHZABIJZCDIJZIJADKJZBCKJZIJZLZWDWGKJZWCLZWB
    WDDCIJZWCKJZKJZWIWCWBWLWGWDKWBWLWCWKKJZWGWBWKEFZWCEFZWLWNLWBVRVOVPWOVNVQVRV
    SUAZVNVOVPVTUBZVNVOVPVTUCZDCMNZWBVJVKVLWPVJVMWAUDZVJVKVLWAUEZVJVKVLWAUJABMN
    ZWKWCOPWBVJVRVMVQWNWGLXAWQVJVMWAUFVNVQVTUGZADBCUKQUHUIWBWDWKKJZWCKJZRWCKJZW
    MWCWBXERWCKWBXECDKJZDCKJZIJZXIXIIJZRWBVOVRVTVQXEXJLWRWQVNVQVTULXDCDDCUKQWBX
    HXIXIIWBVOVRXHXILWRWQCDOPUMWBXIEFZXIGSZXKRLWBVRVOXLWQWRDCTPWBVRVSVOVPXMWQVN
    VQVRVSUNZWRWSDCUOQXIUTPUPUMWBWDEFZWOWPXFWMLWBVOVRVSXOWRWQXNCDMNZWTXCWDWKWCU
    QNWBWPXGWCLXCWCURVAUSVBWBWPWGEFZXOWDGSZWHWJVCXCWBWEEFZWFEFZWFGSZXQWBVJVRXSX
    AWQADTPWBVKVOXTXBWRBCTPVMVQYAVJVTBCUOVDWEWFMNXPWAXRVNCDVEVFWCWGWDVGVHVI $.
    $( [15-Mar-2012] $) $( [2-Aug-2004] $)

  ${
    divmuldiv.1 $e |- A e. CC $.
    divmuldiv.2 $e |- B e. CC $.
    divmuldiv.3 $e |- C e. CC $.
    divmuldiv.4 $e |- D e. CC $.
    divmuldiv.5 $e |- B =/= 0 $.
    divmuldiv.6 $e |- D =/= 0 $.
    $( Multiplication of two ratios.  Theorem I.14 of [Apostol] p. 18. $)
    divmuldivi $p |- ( ( A / B ) x. ( C / D ) ) =
                      ( ( A x. C ) / ( B x. D ) ) $=
      ( cc wcel cc0 wne wa cdiv co cmul wceq pm3.2i divmuldiv mp4an ) AKLCKLBKL
      ZBMNZODKLZDMNZOABPQCDPQRQACRQBDRQPQSEGUCUDFITUEUFHJTACBDUAUB $.
      $( [16-Feb-1995] $)

    $( Swap denominators of two ratios. $)
    divmul13i $p |- ( ( A / B ) x. ( C / D ) ) =
                   ( ( C / B ) x. ( A / D ) ) $=
      ( cmul co cdiv mulcomi oveq1i divmuldivi 3eqtr4ri ) CAKLZBDKLZMLACKLZSMLC
      BMLADMLKLABMLCDMLKLRTSMCAGENOCBADGFEHIJPABCDEFGHIJPQ $.
      $( [6-Aug-1999] $)

    $( Addition of two ratios.  Theorem I.13 of [Apostol] p. 18. $)
    divadddivi $p |- ( ( A / B ) + ( C / D ) ) =
                      ( ( ( A x. D ) + ( B x. C ) ) / ( B x. D ) ) $=
      ( cc wcel cc0 wne cdiv co caddc cmul wceq wa divadddiv mpanl12 mp4an ) BK
      LZBMNZDKLZDMNZABOPCDOPQPADRPBCRPQPBDRPOPSZFIHJAKLCKLUDUETUFUGTTUHEGACBDUA
      UBUC $.
      $( [21-Feb-1995] $)

    divdivdiv.7 $e |- C =/= 0 $.
    $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
    divdivdivi $p |- ( ( A / B ) / ( C / D ) )
                        = ( ( A x. D ) / ( B x. C ) ) $=
      ( cc wcel cc0 wne wa cdiv co cmul pm3.2i wceq divdivdiv mp4an ) ALMBLMZBN
      OZPCLMZCNOZPDLMZDNOZPABQRCDQRQRADSRBCSRQRUAEUDUEFITUFUGGKTUHUIHJTABCDUBUC
      $.
      $( [22-Feb-1995] $)
  $}
  $( Cross-multiply in an equality of ratios.  (Contributed by Mario Carneiro,
     23-Feb-2014.) $)
  divmuleq $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 ) /\
                     ( D e. CC /\ D =/= 0 ) ) ) ->
                   ( ( A / C ) = ( B / D ) <-> ( A x. D ) = ( B x. C ) ) ) $=
    ( cc wcel wa cc0 wne cdiv co cmul wceq divcl 3expb ad2ant2r ad2ant2l mulass
    syl3anc divcan1 wb ax-mulcl mulne0 jca adantl mulcan2 simprll oveq1d eqtr3d
    simprrl mulcom syl2anc oveq2d 3eqtr2d eqeq12d bitr3d ) AEFZBEFZGZCEFZCHIZGZ
    DEFZDHIZGZGZGZACJKZCDLKZLKZBDJKZVILKZMZVHVKMZADLKZBCLKZMVGVHEFZVKEFZVIEFZVI
    HIZGZVMVNUAUQVBVQURVEUQUTVAVQACNOPZURVEVRUQVBURVCVDVRBDNOQZVFWAUSVFVSVTUTVC
    VSVAVDCDUBPCDUCUDUEVHVKVIUFSVGVJVOVLVPVGVHCLKZDLKZVJVOVGVQUTVCWEVJMWBUSUTVA
    VEUGZUSVBVCVDUJZVHCDRSVGWDADLUQVBWDAMZURVEUQUTVAWHACTOPUHUIVGVLVKDCLKZLKZVK
    DLKZCLKZVPVGVIWIVKLVGUTVCVIWIMWFWGCDUKULUMVGVRVCUTWLWJMWCWGWFVKDCRSVGWKBCLU
    RVEWKBMZUQVBURVCVDWMBDTOQUHUNUOUP $.
    $( [23-Feb-2014] $)

  $( The reciprocal of a ratio. $)
  recdiv $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
                ( 1 / ( A / B ) ) = ( B / A ) ) $=
    ( cc wcel cc0 wa c1 cdiv co cmul ax-1cn div1i oveq1i wceq ax-1ne0 divdivdiv
    wne pm3.2i mpanl12 mulid2 syl5eqr oveqan12rd ad2ant2r eqtrd ) ACDZAEQZFBCDZ
    BEQZFFZGABHIZHIZGBJIZGAJIZHIZBAHIZUIUKGGHIZUJHIZUNUPGUJHGKLMGCDZURGEQZFUIUQ
    UNNKURUSKORGGABPSUAUEUGUNUONUFUHUGUEULBUMAHBTATUBUCUD $.
    $( [12-Aug-2007] $) $( [3-Aug-2004] $)

  $( Cancellation of inverted fractions. $)
  divcan6 $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
         ( ( A / B ) x. ( B / A ) ) = 1 ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul recdiv oveq2d wceq divcl 3expb adantlr
    divne0 recid syl2anc eqtr3d ) ACDZAEFZGBCDZBEFZGZGZABHIZJUHHIZKIZUHBAHIZKIJ
    UGUIUKUHKABLMUGUHCDZUHEFUJJNUBUFULUCUBUDUEULABOPQABRUHSTUA $.
    $( [28-Dec-2007] $)

  $( Swap denominators in a division. $)
  divdiv32 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 )
      /\ ( C e. CC /\ C =/= 0 ) ) -> ( ( A / B ) / C ) = ( ( A / C ) / B ) ) $=
    ( cc wcel cc0 wne wa w3a c1 cdiv cmul wceq reccl div23 syl3an2 divrec 3expb
    co 3adant3 oveq1d divcl syl3an1 3impa 3com23 3eqtr4d ) ADEZBDEZBFGZHZCDEZCF
    GZHZIZAJBKSZLSZCKSZACKSZUOLSZABKSZCKSURBKSZUJUGUODEUMUQUSMBNAUOCOPUNUTUPCKU
    GUJUTUPMZUMUGUHUIVBABQRTUAUGUMUJVAUSMZUGUMUJVCUGUMHZUHUIVCVDURDEZUHUIVCUGUK
    ULVEACUBRURBQUCRUDUEUF $.
    $( [28-Dec-2008] $) $( [2-Aug-2004] $)

  $( Cancel equal divisors in a division.  (Contributed by Jeff Hankins,
     29-Sep-2013.) $)
  divcan7 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\ ( C e. CC /\ C =/= 0 )
  ) -> ( ( A / C ) / ( B / C ) ) = ( A / B ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co cmul wceq divdivdiv 3impdir mulcom adantrr
    3adant2 oveq1d divcan5 3eqtrd ) ADEZBDEBFGHZCDEZCFGZHZIZACJKBCJKJKZACLKZCBL
    KZJKZCALKZUJJKABJKUBUFUCUHUKMACBCNOUGUIULUJJUBUFUIULMZUCUBUDUMUEACPQRSABCTU
    A $.
    $( [29-Sep-2013] $)
  $( Cancellation law for division and multiplication.  (Contributed by Scott
     Fenton, 7-Jun-2013.) $)
  dmdcan $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) /\
         C e. CC ) ->
         ( ( A / B ) x. ( C / A ) ) = ( C / B ) ) $=
    ( cc wcel cc0 wne wa cdiv co cmul c1 mulcom syl2anc oveq1d divass syl112anc
    wceq divcl syl3anc simp1l simp3 simp2l simp2r 3eqtr3rd simp1r div23 3eqtr3d
    w3a eqtrd divid 3ad2ant1 mulid2 syl 3eqtrd ) ADEZAFGZHZBDEZBFGZHZCDEZUIZABI
    JZCAIJZKJZAAIJZCBIJZKJZLVHKJZVHVCCVDKJZAIJZAVHKJZAIJZVFVIVCVKVMAIVCACKJZBIJ
    ZCAKJZBIJZVMVKVCVOVQBIVCUPVBVOVQRUPUQVAVBUAZURVAVBUBZACMNOVCUPVBUSUTVPVMRVS
    VTURUSUTVBUCZURUSUTVBUDZACBPQVCVBUPUSUTVRVKRVTVSWAWBCABPQUEOVCVLVEVDKJZVFVC
    VBVDDEZUPUQVLWCRVTVCUPUSUTWDVSWAWBABSTZVSUPUQVAVBUFZCVDAUGQVCVEDEZWDWCVFRVC
    VBUPUQWGVTVSWFCASTWEVEVDMNUJVCUPVHDEZUPUQVNVIRVSVCVBUSUTWHVTWAWBCBSTZVSWFAV
    HAUGQUHURVAVIVJRVBURVGLVHKAUKOULVCWHVJVHRWIVHUMUNUO $.
    $( [7-Jun-2013] $)

  ${
    divdiv23.1 $e |- A e. CC $.
    divdiv23.2 $e |- B e. CC $.
    divdiv23.3 $e |- C e. CC $.
    ${
      divdiv23.4 $e |- B =/= 0 $.
      divdiv23.5 $e |- C =/= 0 $.
      $( Swap denominators in a division. $)
      divdiv32i $p |- ( ( A / B ) / C ) = ( ( A / C ) / B ) $=
        ( c1 cdiv co cmul reccli div23i divreci oveq1i divcli 3eqtr4i ) AIBJKZL
        KZCJKACJKZSLKABJKZCJKUABJKASCDBEGMFHNUBTCJABDEGOPUABACDFHQEGOR $.
        $( [15-Sep-1999] $)
    $}
    $( Swap denominators in a division. $)
    divdiv23zi $p |- ( ( B =/= 0 /\ C =/= 0 ) ->
                ( ( A / B ) / C ) = ( ( A / C ) / B ) ) $=
      ( cc wcel cc0 wne cdiv co wceq wa divdiv32 mp3an1 mpanr1 mpanl1 ) BGHZBIJ
      ZCIJZABKLCKLACKLBKLMZESTNZCGHZUAUBFAGHUCUDUANUBDABCOPQR $.
      $( [15-Sep-1999] $)
  $}

  $( Division into a fraction. $)
  divdiv1 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\
       ( C e. CC /\ C =/= 0 ) ) -> ( ( A / B ) / C ) = ( A / ( B x. C ) ) ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul ax-1cn ax-1ne0 pm3.2i divdivdiv mpanr2
    w3a wceq 3impa div1 oveq2d ad2antrl 3adant1 mulid1 oveq1d 3ad2ant1 3eqtr3d
    ) ADEZBDEBFGHZCDEZCFGZHZRABIJZCKIJZIJZAKLJZBCLJZIJZUNCIJZAURIJZUIUJUMUPUSSZ
    UIUJHUMKDEZKFGZHVBVCVDMNOABCKPQTUJUMUPUTSZUIUKVEUJULUKUOCUNICUAUBUCUDUIUJUS
    VASUMUIUQAURIAUEUFUGUH $.
    $( [31-Dec-2007] $)

  $( Division by a fraction. $)
  divdiv2 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\ ( C e. CC /\ C =/= 0 )
              ) -> ( A / ( B / C ) ) = ( ( A x. C ) / B ) ) $=
    ( cc wcel cc0 wne wa c1 cdiv co cmul ax-1cn ax-1ne0 pm3.2i divdivdiv mpanl2
    w3a wceq 3impb div1 3ad2ant1 oveq1d mulid2 ad2antrl 3adant3 oveq2d 3eqtr3d
    ) ADEZBDEZBFGZHZCDECFGHZRZAIJKZBCJKZJKZACLKZIBLKZJKZAUPJKURBJKUIULUMUQUTSZU
    IIDEZIFGZHULUMHVAVBVCMNOAIBCPQTUNUOAUPJUIULUOASUMAUAUBUCUNUSBURJUIULUSBSZUM
    UJVDUIUKBUDUEUFUGUH $.
    $( [27-Dec-2008] $)

  $( Division into a reciprocal. $)
  recdiv2 $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
                ( ( 1 / A ) / B ) = ( 1 / ( A x. B ) ) ) $=
    ( c1 cc wcel cc0 wne wa cdiv co cmul wceq ax-1cn divdiv1 mp3an1 ) CDEADEAFG
    HBDEBFGHCAIJBIJCABKJIJLMCABNO $.
    $( [19-Oct-2007] $)

  $( Two numbers whose reciprocals sum to 1 are called "conjugates" and satisfy
     this relationship.  Equation 5 of [Kreyszig] p. 12. $)
  conjmul $p |- ( ( ( P e. CC /\ P =/= 0 ) /\ ( Q e. CC /\ Q =/= 0 ) ) ->
      ( ( ( 1 / P ) + ( 1 / Q ) ) = 1 <-> ( ( P - 1 ) x. ( Q - 1 ) ) = 1 ) ) $=
    ( cc wcel cc0 wa cmul co c1 cdiv caddc wceq cmin reccl adantr syl3anc recid
    wne 3eqtrd ad2ant2r simpll simprl oveq1d mulid2 adantl mulass oveq2d mulid1
    mul32 ad2antrl ad2antrr oveq12d mulcl adddi addcom 3eqtr4d eqeq12d wb addcl
    syl2an mulne0 ax-1cn mulcan mp3an2 syl12anc eqcom muleqadd syl5bb 3bitr3d
    syl ) ACDZAERZFZBCDZBERZFZFZABGHZIAJHZIBJHZKHZGHZVRIGHZLZABKHZVRLZWAILZAIMH
    BIMHGHILZVQWBWEWCVRVQVRVSGHZVRVTGHZKHZBAKHZWBWEVQWIBWJAKVQWIAVSGHZBGHZIBGHZ
    BVQVKVNVSCDZWIWNLVKVLVPUAZVMVNVOUBZVMWPVPANZOZABVSUIPVMWNWOLVPVMWMIBGAQUCOV
    NWOBLVMVOBUDUJSVQWJABVTGHZGHZAIGHZAVQVKVNVTCDZWJXBLWQWRVPXDVMBNZUEZABVTUFPV
    PXBXCLVMVPXAIAGBQUGUEVKXCALVLVPAUHUKSULVQVRCDZWPXDWBWKLVKVNXGVLVOABUMZTZWTX
    FVRVSVTUNPVKVNWEWLLVLVOABUOTUPVKVNWCVRLZVLVOVKVNFZXGXJXHVRUHVJTUQVQWACDZXGV
    RERZWDWGURZVMWPXDXLVPWSXEVSVTUSUTXIABVAXLICDXGXMFXNVBWAIVRVCVDVEVKVNWFWHURV
    LVOWFVRWELXKWHWEVRVFABVGVHTVI $.
    $( [12-Nov-2006] $)

  ${
    $d x y B $.
    redivcl.1 $e |- A e. RR $.
    redivcl.2 $e |- B e. RR $.
    redivcl.3 $e |- B =/= 0 $.
    $( Closure law for division of reals. $)
    redivcli $p |- ( A / B ) e. RR $=
      ( vx cdiv co c1 cmul cr recni divreci cv wceq wrex wcel cc wb ax-1cn recn
      cc0 wne ax-rrecex mp2an cif eqeq2 oveq2 eqeq1d bibi12d elimel divmuli syl
      dedth eleq1a sylbird rexlimiv ax-mp remulcli eqeltri ) ABGHAIBGHZJHKABACL
      BDLZEMAVACBFNZJHZIOZFKPZVAKQZBKQBUBUCVFDEFBUDUEVEVGFKVCKQZVEVAVCOZVGVHVCR
      QZVIVESZVCUAVJVKVAVJVCIUFZOZBVLJHZIOZSVCIVCVLOZVIVMVEVOVCVLVAUGVPVDVNIVCV
      LBJUHUIUJIBVLTVBVCIRTUKEULUNUMVCKVAUOUPUQURUSUT $.
      $( [9-May-1999] $)
  $}

  ${
    redivclz.1 $e |- A e. RR $.
    redivclz.2 $e |- B e. RR $.
    $( Closure law for division of reals. $)
    redivclzi $p |- ( B =/= 0 -> ( A / B ) e. RR ) $=
      ( cc0 wne cdiv co cr wcel c1 cif oveq2 eleq1d 1re keepel elimne0 redivcli
      wceq dedth ) BEFZABGHZIJAUABKLZGHZIJBKBUCSUBUDIBUCAGMNAUCCUABKIDOPBQRT $.
      $( [9-May-1999] $)
  $}

  $( Closure law for division of reals. $)
  redivcl $p |- ( ( A e. RR /\ B e. RR /\ B =/= 0 ) -> ( A / B ) e. RR ) $=
    ( cr wcel cc0 wne cdiv co wi cif wceq oveq1 eleq1d imbi2d neeq1 imbi12d 0re
    oveq2 elimel redivclzi dedth2h 3impia ) ACDZBCDZBEFZABGHZCDZUCUDUEUGIUEUCAE
    JZBGHZCDZIUDBEJZEFZUHUKGHZCDZIABEEAUHKZUGUJUEUOUFUICAUHBGLMNBUKKZUEULUJUNBU
    KEOUPUIUMCBUKUHGRMPUHUKAECQSBECQSTUAUB $.
    $( [4-May-2005] $) $( [27-Sep-1999] $)

  ${
    rereccl.1 $e |- A e. RR $.
    rereccl.2 $e |- A =/= 0 $.
    $( Closure law for reciprocal. $)
    rereccli $p |- ( 1 / A ) e. RR $=
      ( c1 1re redivcli ) DAEBCF $.
      $( [30-Apr-2005] $)
  $}

  ${
    rerecclz.1 $e |- A e. RR $.
    $( Closure law for reciprocal. $)
    rerecclzi $p |- ( A =/= 0 -> ( 1 / A ) e. RR ) $=
      ( c1 1re redivclzi ) CADBE $.
      $( [30-Apr-2005] $)
  $}

  $( Closure law for reciprocal. $)
  rereccl $p |- ( ( A e. RR /\ A =/= 0 ) -> ( 1 / A ) e. RR ) $=
    ( c1 cr wcel cc0 wne cdiv co 1re redivcl mp3an1 ) BCDACDAEFBAGHCDIBAJK $.
    $( [30-Apr-2005] $)

  ${
    eqneg.1 $e |- A e. CC $.
    $( A number equal to its negative is zero. $)
    eqnegi $p |- ( A = -u A <-> A = 0 ) $=
      ( cneg wceq cc0 c1 caddc co wne 1re readdcli lt01 addgt0ii gt0ne0ii df-ne
      wn mpbi cmul wo oveq2 1p1timesi negidi eqcomi 3eqtr4g recni mul0ori sylib
      ord mpi cmin df-neg 0cn subidi eqtr2i id negeq 3eqtr4a impbii ) AACZDZAED
      ZUTFFGHZEDZPZVAVBEIVDVBFFJJKZFFJJLLMNVBEOQUTVCVAUTVBARHZEDVCVASUTAAGHAUSG
      HZVFEAUSAGTABUAVGEABUBUCUDVBAVBVEUEBUFUGUHUIVAEECZAUSVHEEUJHEEUKEULUMUNVA
      UOAEUPUQUR $.
      $( [29-May-1999] $)
  $}

  $( A number equal to its negative is zero. $)
  eqneg $p |- ( A e. CC -> ( A = -u A <-> A = 0 ) ) $=
    ( cc wcel cneg wceq cc0 wb cif id negeq eqeq12d eqeq1 bibi12d elimel eqnegi
    0cn dedth ) ABCZAADZEZAFEZGRAFHZUBDZEZUBFEZGAFAUBEZTUDUAUEUFAUBSUCUFIAUBJKA
    UBFLMUBAFBPNOQ $.
    $( [12-Jul-2005] $)

  $( A number is zero iff its negative is zero. $)
  negeq0 $p |- ( A e. CC -> ( A = 0 <-> -u A = 0 ) ) $=
    ( cc wcel cneg wceq cc0 negneg eqeq2d eqcom syl6rbb eqneg negcl syl 3bitr3d
    wb ) ABCZAADZEZQQDZEZAFEQFEZPTQAERPSAQAGHQAIJAKPQBCTUAOALQKMN $.
    $( [12-Jul-2005] $)

  ${
    negne0.1 $e |- A e. CC $.
    $( A number is nonzero iff its negative is nonzero. $)
    negne0bi $p |- ( A =/= 0 <-> -u A =/= 0 ) $=
      ( cc0 cneg cc wcel wceq wb negeq0 ax-mp necon3bii ) ACADZCAEFACGLCGHBAIJK
      $.
      $( [10-Aug-1999] $)
    ${
      negn0.2 $e |- A =/= 0 $.
      $( The negative of a nonzero number is nonzero. $)
      negne0i $p |- -u A =/= 0 $=
        ( cc0 wne cneg negne0bi mpbi ) ADEAFDECABGH $.
        $( [30-Jul-2004] $)
    $}
  $}

  $( Quotient of two negatives.  (Contributed by Paul Chapman, 10-Nov-2012.) $)
  div2neg $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                  ( -u A / -u B ) = ( A / B ) ) $=
    ( cc wcel cc0 wne w3a cneg cdiv co wceq cmul negcl 3ad2ant2 simp1 syl112anc
    c1 simp2 3adant1 3ad2ant1 simp3 divneg syld3an1 negeqd eqtr3d oveq2d ax-1cn
    div12 divid negcli mulcom mpan2 mulm1 3eqtrd negeq0 necon3bid biimpa divmul
    eqtrd wb divcl mpbird ) ACDZBCDZBEFZGZAHZBHZIJABIJZKZVHVILJZVGKZVFVKAVHBIJZ
    LJZAQHZLJZVGVFVHCDZVCVDVEVKVNKVDVCVQVEBMNZVCVDVEOVCVDVERZVCVDVEUAVHABUHPVFV
    MVOALVFBBIJZHZVMVOVDVDVCVEWAVMKVSBBUBUCVFVTQVDVEVTQKVCBUISUDUEUFVCVDVPVGKVE
    VCVPVOALJZVGVCVOCDVPWBKQUGUJAVOUKULAUMUSTUNVFVGCDZVICDVQVHEFZVJVLUTVCVDWCVE
    AMTABVAVRVDVEWDVCVDVEWDVDBEVHEBUOUPUQSVGVIVHURPVB $.
    $( [10-Nov-2012] $)

