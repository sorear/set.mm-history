$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Imaginary and complex number properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The imaginary unit ` _i ` is not a real number. $)
  inelr $p |- -. _i e. RR $=
    ( ci cr wcel cc0 wceq wne wn ine0 df-ne mpbi cmul co clt wbr c1 0re ltnsymi
    lt01 1re caddc cneg renegcli eqeltri ltadd1i ax-1cn addid2i ax-i2m1 breq12i
    ax-mp ixi bitri mtbir msqgt0 ex necon1bd mpi mto ) ABCZADEZADFZUSGHADIJURDA
    AKLZMNZGUSVBODMNZDOMNVCGRDOPSQUIVBDOTLZVAOTLZMNVCDVAOPVAOUABUJOSUBUCSUDVDOV
    EDMOUEUFUGUHUKULURVBADURUTVBAUMUNUOUPUQ $.
    $( [6-May-1999] $)

  ${
    cru.1 $e |- A e. RR $.
    cru.2 $e |- B e. RR $.
    cru.3 $e |- C e. RR $.
    cru.4 $e |- D e. RR $.
    $( Lemma for ~ crui . $)
    crulem $p |- ( ( A + ( _i x. B ) ) = ( C + ( _i x. D ) ) -> B = D ) $=
      ( ci cmul co caddc wceq cmin cc0 cr wcel cneg ax-icn recni inelr wn df-ne
      wne wi cdiv wa renegcli readdcli resubcli redivclzi subdii mulcli negsubi
      eqtr4i negcli negidi oveq1i addcli addid2i 3eqtri oveq2i addid1i divmulzi
      oveq1 add4i 3eqtr3g syl5eq mulcomi eqeq1i syl6bb syl5ibr eleq1d syl5ib ex
      imp pm2.43b syl5bir mt3i subeq0i sylib ) AIBJKZLKZCIDJKZLKZMZBDNKZOMZBDMW
      FWHIPQZUAWHUBWGOUDZWFWIWGOUCWFWJWIWJWFWJWIUEWJCARZLKZWGUFKZPQWJWFUGZWIWLW
      GCWKGAEUHZUIZBDFHUJZUKWNWMIPWJWFWMIMZWFWRWJIWGJKZWLMZWFWSWBWDRZLKZWLWSWBW
      DNKXBIBDSBFTZDHTZULWBWDIBSXCUMZIDSXDUMZUNUOWFWCWKXALKZLKZWEXGLKZXBWLWCWEX
      GLVEXHAWKLKZXBLKOXBLKXBAWBWKXAAETZXEWKWOTZWDXFUPZVFXJOXBLAXKUQURXBWBXAXEX
      MUSUTVAXIWLWDXALKZLKWLOLKWLCWDWKXACGTXFXLXMVFXNOWLLWDXFUQVBWLWLWPTZVCVAVG
      VHWJWRWGIJKZWLMWTWLWGIXOWGWQTZSVDXPWSWLWGIXQSVIVJVKVLVPVMVNVOVQVRVSBDXCXD
      VTWA $.
      $( [11-Mar-2008] $) $( [8-May-1999] $)

    $( The representation of complex numbers in terms of real and imaginary
       parts is unique.  Proposition 10-1.3 of [Gleason] p. 130. $)
    crui $p |- ( ( A + ( _i x. B ) ) = ( C + ( _i x. D ) ) <->
                   ( A = C /\ B = D ) ) $=
      ( ci cmul co caddc wceq wa crulem oveq2 oveq2d eqeq2d recni addcomi sylbi
      ax-icn mulcli eqeq12i addcani biimpi syl6bir mpcom jca oveqan12d impbii
      id ) AIBJKZLKZCIDJKZLKZMZACMZBDMZNUQURUSUSUQURABCDEFGHOZUSUQUNCUMLKZMZURU
      SVAUPUNUSUMUOCLBDIJPZQRVBUMALKZUMCLKZMZURUNVDVAVEAUMAESZIBUBBFSUCZTCUMCGS
      ZVHTUDVFURUMACVHVGVIUEUFUAUGUHUTUIURUSACUMUOLURULVCUJUK $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)
  $}

  $( The representation of complex numbers in terms of real and imaginary parts
     is unique.  Proposition 10-1.3 of [Gleason] p. 130. $)
  cru $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
      ( ( A + ( _i x. B ) ) = ( C + ( _i x. D ) ) <-> ( A = C /\ B = D ) ) ) $=
    ( cr wcel ci cmul co caddc wceq wa cc0 cif oveq1 eqeq1d bibi12d 0re elimel
    wb eqeq1 anbi1d oveq2 oveq2d anbi2d eqeq2d eqeq2 crui dedth4h ) AEFZBEFZCEF
    ZDEFZAGBHIZJIZCGDHIZJIZKZACKZBDKZLZTUJAMNZUNJIZUQKZVBCKZUTLZTVBGUKBMNZHIZJI
    ZUQKZVEVGDKZLZTVIULCMNZUPJIZKZVBVMKZVKLZTVIVMGUMDMNZHIZJIZKZVPVGVRKZLZTABCD
    MMMMAVBKZURVDVAVFWDUOVCUQAVBUNJOPWDUSVEUTAVBCUAUBQBVGKZVDVJVFVLWEVCVIUQWEUN
    VHVBJBVGGHUCUDPWEUTVKVEBVGDUAUEQCVMKZVJVOVLVQWFUQVNVICVMUPJOUFWFVEVPVKCVMVB
    UGUBQDVRKZVOWAVQWCWGVNVTVIWGUPVSVMJDVRGHUCUDUFWGVKWBVPDVRVGUGUEQVBVGVMVRAME
    RSBMERSCMERSDMERSUHUI $.
    $( [11-Mar-2008] $) $( [9-May-1999] $)

  ${
    crne0.1 $e |- A e. RR $.
    crne0.2 $e |- B e. RR $.
    $( The real representation of complex numbers is nonzero iff one of its
       terms is nonzero. $)
    crne0i $p |- ( ( A =/= 0 \/ B =/= 0 ) <-> ( A + ( _i x. B ) ) =/= 0 ) $=
      ( ci cmul co caddc cc0 wceq wn wa wne wo id ax-icn 0cn mulcli syl6eq 0re
      addid2i mul01i eqtr2i crui sylib oveq2 eqtri impbii notbii df-ne 3bitr4ri
      oveqan12d neorian ) AEBFGZHGZIJZKAIJZBIJZLZKUOIMAIMBIMNUPUSUPUSUPUOIEIFGZ
      HGZJUSUPUOIVAUPOVAUTIUTEIPQRUAZEPUBZUCSABIICDTTUDUEUSUOVAIUQURAIUNUTHUQOB
      IEFUFULVAUTIVBVCUGSUHUIUOIUJAIBIUMUK $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    $d x y z w A $.
    $( The real part of a complex number is unique.  Proposition 10-1.3 of
       [Gleason] p. 130. $)
    creur $p |- ( A e. CC -> E! x e. RR E. y e. RR A = ( x + ( _i x. y ) ) ) $=
      ( vz vw wcel cv ci cmul co caddc wceq cr wrex wa wral wex df-rex syl5bi
      wi wreu ax-cnre anbi12i eeanv bitr4i an4 eqtr2 cru simpl syl6bi syl5 an4s
      cc expimpd exlimdvv rgen2a jctir oveq1 eqeq2d rexbidv oveq2 oveq2d syl6bb
      cbvrexv reu4 sylibr ) CUMFZCAGZHBGZIJZKJZLZBMNZAMNZVMCDGZHEGZIJZKJZLZEMNZ
      OZVHVOLZTZDMPAMPZOVMAMUAVGVNWDABCUBWCADMWAVIMFZVLOZVPMFZVSOZOZEQBQZVHMFZV
      OMFZOZWBWAWFBQZWHEQZOWJVMWNVTWOVLBMRVSEMRUCWFWHBEUDUEWMWIWBBEWIWEWGOZVLVS
      OZOWMWBWEVLWGVSUFWMWPWQWBWKWEWLWGWQWBTWQVKVRLZWKWEOWLWGOOZWBCVKVRUGWSWRWB
      VIVPLZOWBVHVIVOVPUHWBWTUIUJUKULUNSUOSUPUQVMVTADMWBVMCVOVJKJZLZBMNVTWBVLXB
      BMWBVKXACVHVOVJKURUSUTXBVSBEMWTXAVRCWTVJVQVOKVIVPHIVAVBUSVDVCVEVF $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number is unique.  Proposition 10-1.3 of
       [Gleason] p. 130. $)
    creui $p |- ( A e. CC -> E! y e. RR E. x e. RR A = ( x + ( _i x. y ) ) ) $=
      ( vz vw wcel cv ci cmul co caddc wceq cr wrex wa wral wex df-rex syl5bi
      wi cc wreu ax-cnre rexcom sylib anbi12i eeanv bitr4i an4 eqtr2 cru syl6bi
      simpr syl5 expcom imp3a exlimdvv rgen2a jctir oveq2 oveq2d eqeq2d rexbidv
      an4s oveq1 cbvrexv syl6bb reu4 sylibr ) CUAFZCAGZHBGZIJZKJZLZAMNZBMNZVPCD
      GZHEGZIJZKJZLZDMNZOZVLVSLZTZEMPBMPZOVPBMUBVJVQWGVJVOBMNAMNVQABCUCVOABMMUD
      UEWFBEMWDVKMFZVOOZVRMFZWBOZOZDQAQZVLMFZVSMFZOZWEWDWIAQZWKDQZOWMVPWQWCWRVO
      AMRWBDMRUFWIWKADUGUHWPWLWEADWLWHWJOZVOWBOZOWPWEWHVOWJWBUIWPWSWTWEWSWPWTWE
      TZWHWNWJWOXAWTVNWALZWHWNOWJWOOOZWECVNWAUJXCXBVKVRLZWEOWEVKVLVRVSUKXDWEUMU
      LUNVDUOUPSUQSURUSVPWCBEMWEVPCVKVTKJZLZAMNWCWEVOXFAMWEVNXECWEVMVTVKKVLVSHI
      UTVAVBVCXFWBADMXDXEWACVKVRVTKVEVBVFVGVHVI $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)
  $}

  $( A real number times the imaginary unit is real only if the number is 0. $)
  rimul $p |- ( ( A e. RR /\ ( _i x. A ) e. RR ) -> A = 0 ) $=
    ( cr wcel ci cmul co wa cc0 wceq caddc recn ax-icn mul01i oveq1i 0cn mulcli
    cc addcom mpan 0re syl5eqr syl adantl wb cru mpanr2 mpanl1 mpbid simprd ) A
    BCZDAEFZBCZGZHUKIZAHIZUMHUKJFZUKDHEFZJFZIZUNUOGZULUSUJULUKQCZUSUKKVAUPUQUKJ
    FZURUQHUKJDLMNUQQCVAVBURIDHLOPUQUKRSUAUBUCHBCZUJULUSUTUDZTVCUJGULVCVDTHAUKH
    UEUFUGUHUI $.
    $( [11-Mar-2008] $) $( [28-May-1999] $)

  ${
    $d x y z A $.
    $( The complex conjugate of a complex number is unique. $)
    cju $p |- ( A e. CC -> E! x e. CC
               ( ( A + x ) e. RR /\ ( _i x. ( A - x ) ) e. RR ) ) $=
      ( vy vz cc wcel caddc co cr ci cmin cmul wa wrex wceq ax-icn oveq2d oveq2
      syl3anc eleq1d cv wi wral wreu ax-cnre mulcl sylancr syl2an adantr adantl
      subcl ppncan ax-addrcl anidms eqeltrd pnncan a1i ax-distr eqtr4d ax-addcl
      recn syl2anc ax-mulass mp3an12 syl c1 cneg ixi 1re renegcli eqeltri simpr
      ax-mulrcl anbi12d rcla4ev syl12anc oveq1 rexbidv syl5ibrcom rexlimivv an4
      resubcl pnpcan 3expb syl5ib ancoms adantrl adantrr nnncan1 3com23 anim12d
      subdi eqtr3d rimul subeq0 biimpd 3syld syl5bi ralrimivva reu4 sylanbrc
      cc0 ) BEFZBAUAZGHZIFZJBXDKHZLHZIFZMZAENZXJBCUAZGHZIFZJBXLKHZLHZIFZMZMZXDX
      LOZUBZCEUCAEUCXJAEUDXCBXLJDUAZLHZGHZOZDINCINXKCDBUEYEXKCDIIXLIFZYBIFZMZXK
      YEYDXDGHZIFZJYDXDKHZLHZIFZMZAENZYHXLYCKHZEFZYDYPGHZIFZJYDYPKHZLHZIFZYOYFX
      LEFZYCEFZYQYGXLVAZYGJEFZYBEFZUUDPYBVAZJYBUFUGZXLYCUKUHYHYRXLXLGHZIYHUUCUU
      DUUCYRUUJOYFUUCYGUUEUIZYGUUDYFUUIUJZUUKXLYCXLULSYFUUJIFZYGYFUUMXLXLUMUNUI
      UOYHUUAJJLHZYBYBGHZLHZIYHUUAJJUUOLHZLHZUUPYHYTUUQJLYHYTYCYCGHZUUQYHUUCUUD
      UUDYTUUSOUUKUULUULXLYCYCUPSYHUUFUUGUUGUUQUUSOUUFYHPUQYGUUGYFUUHUJZUUTJYBY
      BURSUSQYHUUOEFZUUPUUROZYHUUGUUGUVAUUTUUTYBYBUTVBUUFUUFUVAUVBPPJJUUOVCVDVE
      USYHUUNIFUUOIFZUUPIFUUNVFVGIVHVFVIVJVKYHYGYGUVCYFYGVLZUVDYBYBUMVBUUNUUOVM
      UGUOYNYSUUBMAYPEXDYPOZYJYSYMUUBUVEYIYRIXDYPYDGRTUVEYLUUAIUVEYKYTJLXDYPYDK
      RQTVNVOVPYEXJYNAEYEXFYJXIYMYEXEYIIBYDXDGVQTYEXHYLIYEXGYKJLBYDXDKVQQTVNVRV
      SVTVEXCYAACEEXSXFXNMZXIXQMZMZXCXDEFZUUCMZMZXTXFXIXNXQWAUVKUVHXDXLKHZIFZJU
      VLLHZIFZMZUVLXBOZXTUVKUVFUVMUVGUVOUVFXEXMKHZIFUVKUVMXEXMWBUVKUVRUVLIXCUVI
      UUCUVRUVLOBXDXLWCWDTWEUVGXPXHKHZIFZUVKUVOXQXIUVTXPXHWBWFUVKUVSUVNIUVKJXOX
      GKHZLHZUVSUVNUVKUUFXOEFZXGEFZUWBUVSOUUFUVKPUQXCUUCUWCUVIBXLUKWGXCUVIUWDUU
      CBXDUKWHJXOXGWLSUVKUWAUVLJLXCUVIUUCUWAUVLOZXCUUCUVIUWEBXLXDWIWJWDQWMTWEWK
      UVPUVQUBUVKUVLWNUQUVJUVQXTUBXCUVJUVQXTXDXLWOWPUJWQWRWSXJXRACEXTXFXNXIXQXT
      XEXMIXDXLBGRTXTXHXPIXTXGXOJLXDXLBKRQTVNWTXA $.
      $( [6-Nov-2013] $)
  $}


