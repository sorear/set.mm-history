$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Function operation analogue theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x F $.  $d x G $.  $d x V $.
    $( Function analog of ~ subeq0 .  (Contributed by Mario Carneiro,
       24-Jul-2014.) $)
    ofsubeq0 $p |- ( ( A e. V /\ F : A --> CC /\ G : A --> CC ) ->
                     ( ( F oF - G ) = ( A X. { 0 } ) <-> F = G ) ) $=
      ( vx wcel cc wf cmin co cfv cc0 wceq wral wfn ffn syl eqidd wb ffvelrn cv
      w3a cof csn cxp wa simp2 simp3 simp1 inidm ofval 0cn elexi adantl eqeq12d
      fvconst2 sylan subeq0 syl2anc bitrd ralbidva fconst ax-mp eqfnfv2 sylancl
      offn 3bitr4d ) ADFZAGBHZAGCHZUBZEUAZBCIUCJZKZVLALUDZUEZKZMZEANZVLBKZVLCKZ
      MZEANZVMVPMZBCMZVKVRWBEAVKVLAFZUFZVRVTWAIJZLMZWBWGVNWHVQLVKAAVTWAIABCDDVL
      VKVIBAOZVHVIVJUGZAGBPQZVKVJCAOZVHVIVJUHZAGCPQZVHVIVJUIZWPAUJZWGVTRWGWARUK
      WFVQLMVKALVLLGULUMZUPUNUOWGVTGFZWAGFZWIWBSVKVIWFWSWKAGVLBTUQVKVJWFWTWNAGV
      LCTUQVTWAURUSUTVAVKVMAOVPAOZWDVSSVKAAIABCDDWLWOWPWPWQVFAVOVPHXAALWRVBAVOV
      PPVCEAVMVPVDVEVKWJWMWEWCSWLWOEABCVDUSVG $.
      $( [24-Jul-2014] $)

    $( Function analog of ~ negsub .  (Contributed by Mario Carneiro,
       24-Jul-2014.) $)
    ofnegsub $p |- ( ( A e. V /\ F : A --> CC /\ G : A --> CC ) ->
      ( F oF + ( ( A X. { -u 1 } ) oF x. G ) ) = ( F oF - G ) ) $=
      ( vx wcel cc wf cfv cneg caddc c1 cmul cof co cmin wfn ffn syl a1i w3a cv
      csn cxp simp1 simp2 ax-1cn negcli fnconstg ax-mp simp3 inidm offn wa ofc1
      eqidd wceq ffvelrn sylan mulm1 eqtrd negsub syl2anc ofval eqtr4d offveq )
      ADFZAGBHZAGCHZUAZEAEUBZBIZVKCIZJZKBALJZUCUDZCMNOZBCPNOZDVGVHVIUEZVJVHBAQV
      GVHVIUFZAGBRSZVJAAMAVPCDDVPAQZVJVOGFZWBLUGUHZAVOGUIUJTVJVICAQVGVHVIUKZAGC
      RSZVSVSAULZUMVJAAPABCDDWAWFVSVSWGUMVJVKAFZUNZVLUPZWIVKVQIVOVMMOZVNVJAVOVM
      MCDGVKVSWCVJWDTWFWIVMUPZUOWIVMGFZWKVNUQVJVIWHWMWEAGVKCURUSZVMUTSVAWIVLVNK
      OZVLVMPOZVKVRIWIVLGFZWMWOWPUQVJVHWHWQVTAGVKBURUSWNVLVMVBVCVJAAVLVMPABCDDV
      KWAWFVSVSWGWJWLVDVEVF $.
      $( [24-Jul-2014] $)

    $( Function analog of ~ subge0 .  (Contributed by Mario Carneiro,
       24-Jul-2014.) $)
    ofsubge0 $p |- ( ( A e. V /\ F : A --> RR /\ G : A --> RR ) ->
                     ( ( A X. { 0 } ) oR <_ ( F oF - G ) <-> G oR <_ F ) ) $=
      ( vx wcel cr wf cc0 cfv cmin co cle wbr wral ffvelrn sylan wfn cc 0cn w3a
      cv csn cxp cof cofr wa simp2 simp3 subge0 syl2anc ralbidva fnconstg ax-mp
      wb a1i ffn syl simp1 inidm offn elexi fvconst2 adantl eqidd ofval ofrfval
      wceq 3bitr4d ) ADFZAGBHZAGCHZUAZIEUBZBJZVNCJZKLZMNZEAOVPVOMNZEAOAIUCUDZBC
      KUELZMUFZNCBWBNVMVRVSEAVMVNAFZUGZVOGFZVPGFZVRVSUOVMVKWCWEVJVKVLUHZAGVNBPQ
      VMVLWCWFVJVKVLUIZAGVNCPQVOVPUJUKULVMEAAIVQMAVTWADDVTARZVMISFWITAISUMUNUPV
      MAAKABCDDVMVKBARWGAGBUQURZVMVLCARWHAGCUQURZVJVKVLUSZWLAUTZVAWLWLWMWCVNVTJ
      IVHVMAIVNISTVBVCVDVMAAVOVPKABCDDVNWJWKWLWLWMWDVOVEZWDVPVEZVFVGVMEAAVPVOMA
      CBDDWKWJWLWLWMWOWNVGVI $.
      $( [24-Jul-2014] $)
  $}


