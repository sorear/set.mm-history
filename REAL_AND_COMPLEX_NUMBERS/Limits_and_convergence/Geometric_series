$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Geometric series
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d k n A $.  $d k ph $.
    expcnv.1 $e |- ( ph -> A e. CC ) $.
    expcnv.2 $e |- ( ph -> ( abs ` A ) < 1 ) $.
    $( A sequence of powers of a complex number ` A ` with absolute value
       smaller than 1 converges to zero.  (The proof was shortened by Mario
       Carneiro, 26-Apr-2014.) $)
    expcnv $p |- ( ph -> ( n e. NN0 |-> ( A ^ n ) ) ~~> 0 ) $=
      ( cexp co cc0 wbr wceq wa c1 cn wcel a1i cc syl crp cr cle vk cn0 cv cmpt
      cli cvv nnuz cz 1z nn0ex mptex 0cn cfv nnnn0 oveq2 eqid ovex fvmpt oveq1d
      simpr sylan9eqr 0exp adantl eqtrd climconst wne cabs cdiv cmin clt adantr
      wb absrpcl sylan elrp reclt1 sylbi mpbid rpreccl rpre difrp sylancr recnd
      1re divcnv nnex nndivre eqeltrd nnz rpexpcl syl2an caddc rpmulcl peano2re
      cmul nnrp lep1 rpge0 bernneq2 syl3anc letrd rpcnne0 exprec breqtrd lerec2
      3expa syl2anb syl2anc nncn nnne0 recdiv2 breqtrrd 3brtr4d climsqz2 absexp
      jca expcl fveq2d 3eqtr4rd climabs0 biimpar syldan pm2.61dane ) ACUBBCUCZF
      GZUDZHUEIZBHABHJZKZHUAYFLUFMUGLUHNZYIUIOYFUFNZYICUBYEUJUKZOHPNZYIULOYIUAU
      CZMNZKYNYFUMZHYNFGZHYOYIYPBYNFGZYQYOYNUBNZYPYRJZYNUNZCYNYEYRUBYFYDYNBFUOY
      FUPBYNFUQURZQYIBHYNFAYHUTUSVAYOYQHJYIYNVBVCVDVEABHVFZCMBVGUMZYDFGZUDZHUEI
      ZYGAUUCKZHUACMLLUUDVHGZLVIGZVHGZYDVHGZUDZUUFLPUFMUGYJUUHUIOYMUUHULOUUHUUK
      PNUUMHUEIUUHUUKUUHUUKRNZUUKSNZUUHUUJRNZUUNUUHLUUIVJIZUUPUUHUUDLVJIZUUQAUU
      RUUCEVKUUHUUDRNZUURUUQVLZABPNZUUCUUSDBVMVNZUUSUUDSNHUUDVJIKUUTUUDVOUUDVPV
      QQVRUUHLSNUUISNZUUQUUPVLWDUUHUUIRNZUVCUUHUUSUVDUVBUUDVSQZUUIVTQZLUUIWAWBV
      RZUUJVSQUUKVTQZWCUUKCWEQUUFUFNZUUHCMUUEWFUKZOUUHYOKZYNUUMUMZUUKYNVHGZSYOU
      VLUVMJUUHCYNUULUVMMUUMYDYNUUKVHUOUUMUPUUKYNVHUQURVCZUUHUUOYOUVMSNUVHUUKYN
      WGVNWHUVKYNUUFUMZRNZUVOSNUVKUVOUUDYNFGZRYOUVOUVQJZUUHCYNUUEUVQMUUFYDYNUUD
      FUOUUFUPUUDYNFUQURZVCZUUHUUSYNUHNZUVQRNZYOUVBYNWIZUUDYNWJWKZWHZUVOVTQUVKU
      VQUVMUVOUVLTUVKUVQLUUJYNWOGZVHGZUVMTUVKUWFLUVQVHGZTIZUVQUWGTIZUVKUWFUUIYN
      FGZUWHTUVKUWFUWFLWLGZUWKUVKUWFRNZUWFSNZUUHUUPYNRNUWMYOUVGYNWPUUJYNWMWKZUW
      FVTQZUVKUWNUWLSNUWPUWFWNQUVKUWKRNZUWKSNUUHUVDUWAUWQYOUVEUWCUUIYNWJWKUWKVT
      QUVKUWNUWFUWLTIUWPUWFWQQUVKUVCYSHUUITIZUWLUWKTIUUHUVCYOUVFVKYOYSUUHUUAVCU
      UHUWRYOUUHUVDUWRUVEUUIWRQVKUUIYNWSWTXAUUHUUDPNZUUDHVFZKZUWAUWKUWHJZYOUUHU
      USUXAUVBUUDXBQUWCUWSUWTUWAUXBUUDYNXCXFWKXDUVKUWMUWBUWIUWJVLZUWOUWDUWMUWNH
      UWFVJIKUVQSNHUVQVJIKUXCUWBUWFVOUVQVOUWFUVQXEXGXHVRUUHUUJPNUUJHVFKZYNPNZYN
      HVFZKUVMUWGJYOUUHUUPUXDUVGUUJXBQYOUXEUXFYNXIYNXJXPUUJYNXKWKXLUVTUVNXMUVKU
      VPHUVOTIUWEUVOWRQXNAYGUUGAUAYFUUFLUFUFMUGYJAUIOYKAYLOUVIAUVJOAYOKZYPYRPUX
      GYSYTYOYSAUUAVCUUBQZAUVAYSYRPNYODUUABYNXQWKWHUXGYRVGUMZUVQYPVGUMUVOAUVAYS
      UXIUVQJYODUUABYNXOWKUXGYPYRVGUXHXRYOUVRAUVSVCXSXTYAYBYC $.
      $( [26-Apr-2014] $) $( [8-May-2006] $)
  $}

  ${
    $d k n A $.  $d k ph $.  $d k n F $.  $d k n Z $.  $d k M $.
    explecnv.1 $e |- Z = ( ZZ>= ` M ) $.
    explecnv.2 $e |- ( ph -> F e. V ) $.
    explecnv.3 $e |- ( ph -> M e. ZZ ) $.
    explecnv.5 $e |- ( ph -> A e. RR ) $.
    explecnv.4 $e |- ( ph -> ( abs ` A ) < 1 ) $.
    explecnv.6 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
    explecnv.7 $e |- ( ( ph /\ k e. Z ) ->
                       ( abs ` ( F ` k ) ) <_ ( A ^ k ) ) $.
    $( A sequence of terms converges to zero when it is less than powers of a
       number ` A ` whose absolute value is smaller than 1.  (Revised by Mario
       Carneiro, 26-Apr-2014.) $)
    explecnv $p |- ( ph -> F ~~> 0 ) $=
      ( vn cc0 wbr cfv cn0 wcel cli cv cabs cmpt cexp co cle cif cc cvv eqid cz
      cuz 0z ifcl sylancr 0cn a1i recnd expcnv fvex eqeltri mptex wa wceq nn0uz
      cr cin ineq12i uzin sylancl syl5req eleq2d biimpa elin sylib simprd oveq2
      fvmpt syl adantr reexpcl syl2anc eqeltrd simpld fveq2 fveq2d syldan abscl
      ovex 3brtr4d absge0 breqtrrd climsqz2 adantl climabs0 mpbird ) ADPUAQOGOU
      BZDRZUCRZUDZPUAQAPCOSBWRUEUFZUDZXAEPUGQZPEUHZUIUJXEUMRZXFUKAPULTZEULTZXEU
      LTUNJXDPEULUOUPPUITAUQURABOABKUSLUTXAUJTAOGWTGEUMRZUJHEUMVAVBVCURZACUBZXF
      TZVDZXKXCRZBXKUEUFZVGXMXKSTZXNXOVEXMXKGTZXPXMXKGSVHZTZXQXPVDAXLXSAXFXRXKA
      XRXIPUMRZVHZXFGXISXTHVFVIAXHXGYAXFVEJUNEPVJVKVLVMVNXKGSVOVPZVQZOXKXBXOSXC
      WRXKBUEVRXCUKBXKUEWJVSVTZXMBVGTZXPXOVGTAYEXLKWAYCBXKWBWCWDXMXKXARZXKDRZUC
      RZVGXMXQYFYHVEZXMXQXPYBWEZOXKWTYHGXAWRXKVEWSYGUCWRXKDWFWGXAUKYGUCVAVSZVTZ
      XMYGUITZYHVGTAXLXQYMYJMWHZYGWIVTWDXMYHXOYFXNUGAXLXQYHXOUGQYJNWHYLYDWKXMPY
      HYFUGXMYMPYHUGQYNYGWLVTYLWMWNACDXAEFUJGHJIXJMXQYIAYKWOWPWQ $.
      $( [26-Apr-2014] $) $( [19-Jul-2008] $)
  $}

  ${
    $d j k A $.  $d j k N $.  $d j k ph $.
    geoser.1 $e |- ( ph -> A e. CC ) $.
    geoser.2 $e |- ( ph -> A =/= 1 ) $.
    geoser.3 $e |- ( ph -> N e. NN0 ) $.
    $( The value of the finite geometric series ` 1 + A ^ 1 + A ^ 2 + ` ...
       ` + A ^ ( N - 1 ) ` .  (The proof was shortened by Mario Carneiro,
       15-Jun-2014.) $)
    geoser $p |- ( ph -> sum_ k e. ( 0 ... ( N - 1 ) ) ( A ^ k ) =
                    ( ( 1 - ( A ^ N ) ) / ( 1 - A ) ) ) $=
      ( c1 cexp co cmin cc0 wceq cmul cc wcel ax-1cn cn0 syl oveq2 cdiv cfz csu
      vj cv fzfid subcl sylancr elfznn0 expcl syl2an fsummulc1 caddc a1i adantr
      wa subdi syl3anc mulid1 expp1 eqcomd oveq12d eqtrd sumeq2dv nn0z peano2zm
      cz cuz cfv zcn npcan sylancl nn0uz syl6eleq eqeltrd fsumtscop exp0 oveq2d
      3eqtrd eqtr2d wne syl2anc fsumcl necomd subeq0 necon3bid mpbird syl112anc
      wb divmul3 ) AHBDIJZKJZHBKJZUAJZLDHKJZUBJZBCUEZIJZCUCZAWNWSMZWLWSWMNJZMZA
      XAWPWRWMNJZCUCZWLAWPWRWMCALWOUFZAHOPZBOPZWMOPZQEHBUGUHZAXGWQRPZWROPZWQWPP
      ZEWQWOUIZBWQUJUKZULAXDWPWRBWQHUMJZIJZKJZCUCBLIJZBWOHUMJZIJZKJWLAWPXCXQCAX
      LUPZXCWRHNJZWRBNJZKJZXQYAXKXFXGXCYDMXNXFYAQUNAXGXLEUOWRHBUQURYAYBWRYCXPKY
      AXKYBWRMXNWRUSSYAXPYCAXGXJXPYCMXLEXMBWQUTUKVAVBVCVDABUDUEZIJZWRXPXRCUDXTL
      WOYEWQBITYEXOBITYELBITYEXSBITADVGPZWOVGPADRPZYGGDVESZDVFSAXSDLVHVIZADOPZX
      FXSDMAYGYKYIDVJSQDHVKVLZADRYJGVMVNVOAXGYERPYFOPYELXSUBJPEYEXSUIBYEUJUKVPA
      XRHXTWKKAXGXRHMEBVQSAXSDBIYLVRVBVSVTAWLOPZWSOPXHWMLWAZWTXBWIAXFWKOPZYMQAX
      GYHYOEGBDUJWBHWKUGUHAWPWRCXEXNWCXIAYNHBWAABHFWDAWMLHBAXFXGWMLMHBMWIQEHBWE
      UHWFWGWLWSWMWJWHWGVA $.
      $( [15-Jun-2014] $) $( [12-May-2006] $)
  $}

  ${
    $d j k n A $.  $d j k F $.  $d j k M $.  $d j k ph $.
    geolim.1 $e |- ( ph -> A e. CC ) $.
    geolim.2 $e |- ( ph -> ( abs ` A ) < 1 ) $.
    ${
      geolim.3 $e |- ( ( ph /\ k e. NN0 ) -> ( F ` k ) = ( A ^ k ) ) $.
      $( The partial sums in the infinite series ` 1 + A ^ 1 + A ^ 2 ` ...
         converge to ` ( 1 / ( 1 - A ) ) ` . $)
      geolim $p |- ( ph -> seq 0 ( + , F ) ~~> ( 1 / ( 1 - A ) ) ) $=
        ( vn cc0 c1 cmin co cdiv cn0 cexp cc wcel cmul cfv wceq vj caddc cli cv
        cseq cmpt cvv nn0uz cz a1i 0cn expcnv wne ax-1cn subcl sylancr cabs clt
        0z wbr 1re ltnri fveq2 1nn0 cle nn0ge0 absid ax-mp syl6eq breq1d mtbiri
        cr necon2ai necomd wb subeq0 necon3bid mpbird divcl syl3anc nn0ex mptex
        wa oveq2 eqid ovex fvmpt adantl expcl sylan eqeltrd expp1 adantr mulcom
        syl syl2anc eqtrd oveq1d div23 syl112anc oveq1 oveq2d 3eqtr4d climmulc2
        mul01 breqtrd reccl seqex peano2nn0 cfz csu nn0cn pncan sylancl sumeq1d
        syl2an divsubdir geoser simpll elfznn0 simpr syl6eleq fsumser climsubc2
        cuz 3eqtr3rd subid1 ) AUBDIUEZJJBKLZMLZIKLZYJUCAIYJUAHNBHUDZJUBLZOLZYIM
        LZUFZYHIPUGNUHIUIQAUSUJZIPQAUKUJZAYPBYIMLZIRLZIUCAIYSUAHNBYLOLZUFZYPIPU
        GNUHYQYRABHEFULABPQZYIPQZYIIUMZYSPQZEAJPQZUUCUUDUNEJBUOUPZAUUEJBUMABJAB
        UQSZJURUTZBJUMZFUUJBJBJTZUUJJJURUTJVAVBUULUUIJJURUULUUIJUQSZJBJUQVCJNQZ
        UUMJTZVDUUNJVLQIJVEUTUUOVAJVFJVGUPVHVIVJVKVMWOZVNAYIIJBAUUGUUCYIITJBTVO
        UNEJBVPUPVQVRZBYIVSVTZYPUGQAHNYOWAWBUJAUAUDZNQZWCZUUSUUBSZBUUSOLZPUUTUV
        BUVCTAHUUSUUAUVCNUUBYLUUSBOWDUUBWEBUUSOWFWGWHZAUUCUUTUVCPQZEBUUSWIWJZWK
        UVABUUSJUBLZOLZYIMLZYSUVCRLZUUSYPSZYSUVBRLUVAUVIBUVCRLZYIMLZUVJUVAUVHUV
        LYIMUVAUVHUVCBRLZUVLAUUCUUTUVHUVNTEBUUSWLWJUVAUVEUUCUVNUVLTUVFAUUCUUTEW
        MZUVCBWNWPWQWRUVAUUCUVEUUDUUEUVMUVJTUVOUVFAUUDUUTUUHWMZAUUEUUTUUQWMZBUV
        CYIWSWTWQUUTUVKUVITAHUUSYOUVINYPYLUUSTZYNUVHYIMUVRYMUVGBOYLUUSJUBXAXBWR
        YPWEUVHYIMWFWGWHZUVAUVBUVCYSRUVDXBXCXDAUUFYTITUURYSXEWOXFAUUDUUEYJPQZUU
        HUUQYIXGWPZYHUGQAUBDIXHUJUVAUVKUVIPUVSUVAUVHPQZUUDUUEUVIPQAUUCUVGNQZUWB
        UUTEUUSXIZBUVGWIXPZUVPUVQUVHYIVSVTWKUVAIUVGJKLZXJLZBCUDZOLZCXKZIUUSXJLZ
        UWICXKYJUVKKLZUUSYHSUVAUWGUWKUWICUVAUWFUUSIXJUVAUUSPQZUUGUWFUUSTUUTUWMA
        UUSXLWHUNUUSJXMXNXBXOUVAJUVHKLYIMLZYJUVIKLZUWJUWLUVAUUGUWBUUDUUEUWNUWOT
        UUGUVAUNUJUWEUVPUVQJUVHYIXQWTUVABCUVGUVOAUUKUUTUUPWMUUTUWCAUWDWHXRUVAUV
        KUVIYJKUVSXBXCUVAUWICDIUUSUVAUWHUWKQZWCZAUWHNQZUWHDSUWITAUUTUWPXSZUWPUW
        RUVAUWHUUSXTWHZGWPUVAUUSNIYESAUUTYAUHYBUWQUUCUWRUWIPQUWQAUUCUWSEWOUWTBU
        WHWIWPYCYFYDAUVTYKYJTUWAYJYGWOXF $.
        $( [15-May-2006] $)
    $}

    geolim2.3 $e |- ( ph -> M e. NN0 ) $.
    geolim2.4 $e |- ( ( ph /\ k e. ( ZZ>= ` M ) ) -> ( F ` k ) = ( A ^ k ) ) $.
    $( The partial sums in the geometric series ` A ^ M + A ^ ( M + 1 ) ` ...
       converge to ` ( ( A ^ M ) / ( 1 - A ) ) ` .  (Contributed by Mario
       Carneiro, 26-Apr-2014.) $)
    geolim2 $p |- ( ph -> seq M ( + , F ) ~~> ( ( A ^ M ) / ( 1 - A ) ) ) $=
      ( caddc cfv cexp co c1 cmin cn0 wcel cc wceq cc0 vn cseq cuz csu cdiv cli
      vj cv eqid cz nn0z syl adantr eluznn0 sylan expcl syl2anc cmpt oveq2 ovex
      wa cdm fvmpt eqtr4d seqfeq adantl geolim seqex breldm nn0uz eqeltrd mpbid
      wbr iserex eqeltrrd isumclim2 cfz isumsplit cvv a1i isumclim cabs clt wne
      0z 1re ltnri fveq2 cr cle nn0ge0 absid sylancr ax-mp syl6eq breq1d mtbiri
      1nn0 necon2ai geoser oveq1d 3eqtr3d ax-1cn necomd subeq0 necon3bid mpbird
      subcl wb divsubdir syl112anc nncan eqtr3d syl3anc isumcl 3eqtr3rd breqtrd
      divcl pncan2 ) AJDEUBZEUCKZBCUHZLMZCUDZBELMZNBOMZUEMZUFAYCCDEYAYAUIZAEPQZ
      EUJQHEUKULZIAYBYAQZVAZBRQZYBPQZYCRQZAYMYKFUMAYIYKYNHYBEUNUOZBYBUPZUQZAJUA
      PBUAUHZLMZURZEUBZXTUFVBZAJCUUADEYJYLYBUUAKZYCYBDKYLYNUUDYCSZYPUAYBYTYCPUU
      AYSYBBLUSUUAUIZBYBLUTVCZULZIVDVEAJUUATUBZUUCQZUUBUUCQAUUINYFUEMZUFVMUUJAB
      UGUUAFGUGUHZPQZUULUUAKZBUULLMZSAUAUULYTUUOPUUAYSUULBLUSUUFBUULLUTVCVFZVGZ
      UUIUUKUFJUUATVHVIULZAUGUUATEPVJHAUUMVAUUNUUORUUPAYMUUMUUORQFBUULUPUOVKVNV
      LZVOVPAUUKNYEOMZYFUEMZOMZUVAYDJMZUVAOMZYGYDAUUKUVCUVAOAPYCCUDTENOMVQMYCCU
      DZYDJMUUKUVCAYCCUUATEYAPVJYHHYNUUEAUUGVFZAYMYNYOFYQUOZUURVRAYCUUKCUUATVSP
      VJTUJQAWEVTUVFUVGUUKVSQANYFUEUTVTUUQWAAUVEUVAYDJABCEFABWBKZNWCVMZBNWDGUVI
      BNBNSZUVINNWCVMNWFWGUVJUVHNNWCUVJUVHNWBKZNBNWBWHNPQZUVKNSZWRUVLNWIQTNWJVM
      UVMWFNWKNWLWMWNWOWPWQWSULZHWTXAXBXAANUUTOMZYFUEMZUVBYGANRQZUUTRQZYFRQZYFT
      WDZUVPUVBSUVQAXCVTAUVQYERQZUVRXCAYMYIUWAFHBEUPUQZNYEXHWMZAUVQYMUVSXCFNBXH
      WMZAUVTNBWDABNUVNXDAYFTNBAUVQYMYFTSNBSXIXCFNBXEWMXFXGZNUUTYFXJXKAUVOYEYFU
      EAUVQUWAUVOYESXCUWBNYEXLWMXAXMAUVARQZYDRQUVDYDSAUVRUVSUVTUWFUWCUWDUWEUUTY
      FXRXNAYCCUUAEYAYHYJUUHYRUUSXOUVAYDXSUQXPXQ $.
      $( [26-Apr-2014] $) $( [6-Jun-2006] $)
  $}

  ${
    $d k A $.  $d k F $.  $d k ph $.
    georeclim.1 $e |- ( ph -> A e. CC ) $.
    georeclim.2 $e |- ( ph -> 1 < ( abs ` A ) ) $.
    georeclim.3 $e |- ( ( ph /\ k e. NN0 ) ->
                        ( F ` k ) = ( ( 1 / A ) ^ k ) ) $.
    $( The limit of a geometric series of reciprocals.  (Contributed by Paul
       Chapman, 28-Dec-2007.)  (Revised by Mario Carneiro, 26-Apr-2014.) $)
    georeclim $p |- ( ph -> seq 0 ( + , F ) ~~> ( A / ( A - 1 ) ) ) $=
      ( cc0 c1 cdiv co cmin cc wcel cabs cfv clt wbr wceq 1re caddc cli wne cle
      cseq 0re lt01 ltleii lenlti mpbi fveq2 abs0 syl6eq breq2d mtbiri necon2ai
      wn syl reccl syl2anc ax-1cn a1i absdiv syl3anc absid mp2an oveq1i absrpcl
      cr wb wa elrp recgt1 sylbi mpbid eqbrtrd geolim divsubdir syl112anc divid
      crp oveq1d eqtrd oveq2d subcl sylancl ltnri subeq0 mpbird recdiv syl22anc
      necon3bid eqtr3d breqtrd ) AUADHUEIIIBJKZLKZJKZBBILKZJKZUBAWOCDABMNZBHUCZ
      WOMNEAIBOPZQRZXAFXCBHBHSZXCIHQRZHIUDRZXEUQHIUFTUGUHZHIUFTUIUJXDXBHIQXDXBH
      OPHBHOUKULUMUNUOUPURZBUSUTAWOOPZIXBJKZIQAXIIOPZXBJKZXJAIMNZWTXAXIXLSXMAVA
      VBZEXHIBVCVDXKIXBJIVINXFXKISTXGIVEVFZVGUMAXCXJIQRZFAXBWANZXCXPVJZAWTXAXQE
      XHBVHUTXQXBVINHXBQRVKXRXBVLXBVMVNURVOVPGVQAIWRBJKZJKZWQWSAXSWPIJAXSBBJKZW
      OLKZWPAWTXMWTXAXSYBSEXNEXHBIBVRVSAYAIWOLAWTXAYAISEXHBVTUTWBWCWDAWRMNZWRHU
      CZWTXAXTWSSAWTXMYCEVABIWEWFAYDBIUCZAXCYEFXCBIBISZXCIIQRITWGYFXBIIQYFXBXKI
      BIOUKXOUMUNUOUPURAWRHBIAWTXMWRHSYFVJEVABIWHWFWLWIEXHWRBWJWKWMWN $.
      $( [26-Apr-2014] $) $( [28-Dec-2007] $)
  $}

  ${
    $d j k A $.  $d j k N $.
    $( The value of the finite geometric series ` 2 ^ -u 1 + 2 ^ -u 2 + ` ...
       ` + 2 ^ -u N ` , multiplied by a constant.  (Contributed by Mario
       Carneiro, 17-Mar-2014.) $)
    geo2sum $p |- ( ( N e. NN /\ A e. CC ) ->
                    sum_ k e. ( 1 ... N ) ( A / ( 2 ^ k ) ) =
                                 ( A - ( A / ( 2 ^ N ) ) ) ) $=
      ( vj cn wcel cc c1 co c2 cexp cdiv cmin cc0 cmul a1i sylancr syl3anc wceq
      syl wa cfz cv csu caddc cz 1z nnz adantr wne simplr cn0 2nn elfznn adantl
      nnnn0 nnexpcl nnne0 divcl oveq2 oveq2d fsumshftm ax-1cn subidi sumeq1i cr
      nncn oveq1i nnrecre ax-mp recni elfznn0 expcl 2ne0 divrec expp1 peano2zdi
      elfzelz exprec 3eqtr2rd peano2nn0 div12 syl112anc 3eqtr4d sumeq2dv halfcl
      2cn fzfid fsummulc1 eqtr4d syl5eq 2halves subaddrii oveq12d simpr divrec2
      1re nndivre recnd subcl 0re halfgt0 ltneii mulass divcan1 3eqtr2d halflt1
      oveq1d necomi geoser mulid2 eqcomd subdir 3eqtrd ) CEFZAGFZUAZHCUBIZAJBUC
      ZKIZLIZBUDHHMIZCHMIZUBIZAJDUCZHUEIZKIZLIZDUDZNYCUBIZHJLIZYEKIZDUDZAJLIZOI
      ZAAJCKIZLIZMIZXQYAYHBDHHCHUFFXQUGPZYSXOCUFFZXPCUHUIZXQXSXRFZUAZXPXTGFZXTN
      UJZYAGFXOXPUUBUKUUCXTEFZUUDUUCJEFZXSULFZUUFUMUUCXSEFZUUHUUBUUIXQXSCUNUOXS
      UPTJXSUQQZXTVGTUUCUUFUUEUUJXTURTAXTUSRXSYFSXTYGALXSYFJKUTVAVBXQYIYJYHDUDZ
      YOYDYJYHDYBNYCUBHVCVDVHVEXQUUKYJYLYNOIZDUDYOXQYJYHUULDXQYEYJFZUAZAHYGLIZO
      IZAYLJLIZOIZYHUULUUNUUOUUQAOUUNUUQYLYKOIZYKYFKIZUUOUUNYLGFZJGFZJNUJZUUQUU
      SSUUNYKGFZYEULFZUVAYKUUGYKVFFUMJVIVJZVKZUUMUVEXQYEYCVLUOZYKYEVMQZUVBUUNWG
      PZUVCUUNVNPZYLJVORUUNUVDUVEUUTUUSSUVGUVHYKYEVPQUUNUVBUVCYFUFFZUUTUUOSUVJU
      VKUUMUVLXQUUMYEYENYCVRVQUOJYFVSRVTVAUUNXPYGGFZYGNUJZYHUUPSXOXPUUMUKZUUNYG
      EFZUVMUUNUUGYFULFZUVPUMUUNUVEUVQUVHYEWATJYFUQQZYGVGTUUNUVPUVNUVRYGURTAYGV
      ORUUNUVAXPUVBUVCUULUURSUVIUVOUVJUVKYLAJWBWCWDWEXQYJYLYNDXQNYCWHXPYNGFXOAW
      FUOUVIWIWJWKXQHYKCKIZMIZHYKMIZLIZYNOIZHHYPLIZMIZAOIZYOYRXQUWCUWEYKLIZYKAO
      IZOIZUWGYKOIZAOIZUWFXQUWBUWGYNUWHOXQUVTUWEUWAYKLXQUVSUWDHMXQUVBUVCYTUVSUW
      DSUVBXQWGPZUVCXQVNPZUUAJCVSRVAUWAYKSXQHYKYKVCUVGUVGHGFZYKYKUEIHSVCHWLVJWM
      PWNXQXPUVBUVCYNUWHSXOXPWOZUWLUWMAJWPRWNXQUWGGFZUVDXPUWKUWISXQUWEGFZUVDYKN
      UJZUWPXQUWNUWDGFZUWQVCXQUWDXQHVFFYPEFZUWDVFFWQXQUUGCULFZUWTUMXOUXAXPCUPUI
      ZJCUQQZHYPWRQWSZHUWDWTQZUVDXQUVGPZUWRXQNYKXAUVFXBXCPZUWEYKUSRUXFUWOUWGYKA
      XDRXQUWJUWEAOXQUWQUVDUWRUWJUWESUXEUXFUXGUWEYKXERXHXFXQYMUWBYNOXQYKDCUXFYK
      HUJXQHYKYKHUVFWQXGXCXIPUXBXJXHXQYRHAOIZUWDAOIZMIZUWFXQAUXHYQUXIMXQUXHAXPU
      XHASXOAXKUOXLXQXPYPGFZYPNUJZYQUXISUWOXQUWTUXKUXCYPVGTXQUWTUXLUXCYPURTAYPW
      PRWNXQUWNUWSXPUWFUXJSUWNXQVCPUXDUWOHUWDAXMRWJWDXN $.
      $( [26-Apr-2014] $) $( [17-Mar-2014] $)
  $}

  ${
    $d j k n A $.  $d j n F $.
    geo2lim.1 $e |- F = ( k e. NN |-> ( A / ( 2 ^ k ) ) ) $.
    $( The value of the infinite geometric series
       ` 2 ^ -u 1 + 2 ^ -u 2 + ` ... , multiplied by a constant.  (Contributed
       by Mario Carneiro, 15-Jun-2014.) $)
    geo2lim $p |- ( A e. CC -> seq 1 ( + , F ) ~~> A ) $=
      ( vn cc wcel c1 cc0 cmin co cvv cn a1i c2 cdiv cexp cfv wceq syl vj caddc
      cseq cli nnuz cz 1z 0cn cmul cn0 cv cmpt cr 1re rehalfcl ax-mp recni cabs
      clt wbr cle 0re halfgt0 ltleii absid mp2an halflt1 eqbrtri expcnv id nnex
      mptex eqeltri nnnn0 adantl oveq2 eqid ovex fvmpt nnz wne 2cn 2ne0 mp3an12
      exprec eqtrd 2nn nnexpcl sylancr nnrecre recnd eqeltrd simpl nnne0 divrec
      wa syl3anc oveq2d 3eqtr4d climmulc2 mul01 breqtrd seqex divcl cfz geo2sum
      nncn csu ancoms elfznn cuz simpr syl6eleq simpll fsumser climsubc2 subid1
      3eqtr2rd ) AFGZUBCHUCZAIJKAUDXSIAUACXTHFLMUEHUFGXSUGNZIFGXSUHNZXSCAIUIKIU
      DXSIAUABUJHOPKZBUKZQKZULZCHFLMUEYAYBXSYCBYCFGXSYCHUMGYCUMGZUNHUOUPZUQNYCU
      RRZHUSUTXSYIYCHUSYGIYCVAUTYIYCSYHIYCVBYHVCVDYCVEVFVGVHNVIXSVJZCLGXSCBMAOY
      DQKZPKZULLDBMYLVKVLVMNXSUAUKZMGZWPZYMYFRZHOYMQKZPKZFYOYPYCYMQKZYRYOYMUJGZ
      YPYSSYNYTXSYMVNVOZBYMYEYSUJYFYDYMYCQVPYFVQYCYMQVRVSTYOYMUFGZYSYRSZYNUUBXS
      YMVTVOOFGOIWAUUBUUCWBWCOYMWEWDTWFZYOYRYOYQMGZYRUMGYOOMGZYTUUEWGUUAOYMWHWI
      ZYQWJTWKWLYOAYQPKZAYRUIKZYMCRZAYPUIKYOXSYQFGZYQIWAZUUHUUISXSYNWMZYOUUEUUK
      UUGYQXGTZYOUUEUULUUGYQWNTZAYQWOWQYNUUJUUHSXSBYMYLUUHMCYDYMSYKYQAPYDYMOQVP
      WRDAYQPVRVSVOZYOYPYRAUIUUDWRWSWTAXAXBYJXTLGXSUBCHXCNYOUUJUUHFUUPYOXSUUKUU
      LUUHFGUUMUUNUUOAYQXDWQWLYOAUUJJKAUUHJKZHYMXEKZAOEUKZQKZPKZEXHZYMXTRYOUUJU
      UHAJUUPWRYNXSUVBUUQSAEYMXFXIYOUVAECHYMYOUUSUURGZWPZUUSMGZUUSCRUVASUVCUVEY
      OUUSYMXJVOZBUUSYLUVAMCYDUUSSYKUUTAPYDUUSOQVPWRDAUUTPVRVSTYOYMMHXKRXSYNXLU
      EXMUVDXSUUTFGZUUTIWAZUVAFGXSYNUVCXNUVDUUTMGZUVGUVDUVEUVIUVFUVEUUFUUSUJGUV
      IWGUUSVNOUUSWHWITZUUTXGTUVDUVIUVHUVJUUTWNTAUUTXDWQXOXRXPAXQXB $.
      $( [15-Jun-2014] $)
  $}

  ${
    $d k n A $.  $d k n R $.
    $( The infinite sum of ` 1 + A ^ 1 + A ^ 2 ` ... is
       ` ( 1 / ( 1 - A ) ) ` . $)
    geoisum $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN0 ( A ^ k ) = ( 1 / ( 1 - A ) ) ) $=
      ( vn cc wcel cabs cfv c1 clt wbr wa cv cexp co cdiv cn0 cc0 cvv a1i ovex
      cmin cmpt nn0uz cz wceq oveq2 eqid fvmpt adantl expcl adantlr simpl simpr
      0z geolim isumclim ) ADEZAFGHIJZKZABLZMNZHHAUANZONZBCPACLZMNZUBZQRPUCQUDE
      USUNSUTPEZUTVFGVAUEUSCUTVEVAPVFVDUTAMUFVFUGAUTMTUHUIZUQVGVADEURAUTUJUKVCR
      EUSHVBOTSUSABVFUQURULUQURUMVHUOUP $.
      $( [26-Apr-2014] $) $( [15-May-2006] $)

    $( The infinite sum of reciprocals
       ` 1 + ( 1 / A ) ^ 1 + ( 1 / A ) ^ 2 ` ... is ` A / ( A - 1 ) ` .
       (Contributed by rpenner, 3-Nov-2007.)  (Revised by Mario Carneiro,
       26-Apr-2014.) $)
    geoisumr $p |- ( ( A e. CC /\ 1 < ( abs ` A ) ) ->
              sum_ k e. NN0 ( ( 1 / A ) ^ k ) = ( A / ( A - 1 ) ) ) $=
      ( vn cc wcel c1 cabs cfv clt wbr cdiv co cexp cn0 cc0 cvv a1i wceq ovex
      cv wa cmin cmpt nn0uz cz 0z oveq2 eqid fvmpt adantl wne cle wn 0re ltleii
      1re lt01 lenlti mpbi fveq2 abs0 syl6eq breq2d mtbiri necon2ai reccl expcl
      sylan2 sylan simpl simpr georeclim isumclim ) ADEZFAGHZIJZUAZFAKLZBTZMLZA
      AFUBLZKLZBCNVRCTZMLZUCZOPNUDOUEEVQUFQVSNEZVSWEHVTRVQCVSWDVTNWEWCVSVRMUGWE
      UHVRVSMSUIUJZVQVRDEZWFVTDEVPVNAOUKWHVPAOAORZVPFOIJZOFULJWJUMOFUNUPUQUOOFU
      NUPURUSWIVOOFIWIVOOGHOAOGUTVAVBVCVDVEAVFVHVRVSVGVIWBPEVQAWAKSQVQABWEVNVPV
      JVNVPVKWGVLVM $.
      $( [26-Apr-2014] $) $( [3-Nov-2007] $)

    $( The infinite sum of ` A ^ 1 + A ^ 2 ` ... is ` ( A / ( 1 - A ) ) ` . $)
    geoisum1 $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN ( A ^ k ) = ( A / ( 1 - A ) ) ) $=
      ( vn cc wcel cabs cfv c1 clt wbr cn cv cexp co cdiv cvv a1i wceq ovex cn0
      wa csu cmin cmpt nnuz cz oveq2 eqid fvmpt adantl simpl nnnn0 expcl syl2an
      simpr 1nn0 cuz elnnuz sylan2br geolim2 isumclim exp1 adantr oveq1d eqtrd
      1z ) ADEZAFGHIJZUAZKABLZMNZBUBAHMNZHAUCNZONZAVMONVIVKVNBCKACLZMNZUDZHPKUE
      HUFEVIVFQVJKEZVJVQGVKRZVICVJVPVKKVQVOVJAMUGVQUHAVJMSUIUJZVIVGVJTEVKDEVRVG
      VHUKZVJULAVJUMUNVNPEVIVLVMOSQVIABVQHWAVGVHUOHTEVIUPQVJHUQGEVIVRVSVJURVTUS
      UTVAVIVLAVMOVGVLARVHAVBVCVDVE $.
      $( [26-Apr-2014] $) $( [1-Nov-2007] $)

    $( The infinite sum of ` A x. ( R ^ 1 ) + A x. ( R ^ 2 ) ` ... is
       ` ( A x. R ) / ( 1 - R ) ` . $)
    geoisum1c $p |- ( ( A e. CC /\ R e. CC /\ ( abs ` R ) < 1 ) ->
          sum_ k e. NN ( A x. ( R ^ k ) ) = ( ( A x. R ) / ( 1 - R ) ) ) $=
      ( vn cc wcel cabs cfv c1 clt wbr cmul co cdiv cn cexp cc0 wceq 1re cli cv
      w3a cmin csu wne simp1 simp2 ax-1cn subcl sylancr simp3 ltnri cr cle lt01
      0re ltleii absid mp2an fveq2 syl5eqr breq1d necon2ai syl subeq0 necon3bid
      mtbii wb wa mpbird divass syl112anc geoisum1 3adant1 oveq2d cmpt nnuz a1i
      cz 1z oveq2 eqid ovex fvmpt adantl cn0 nnnn0 expcl syl2an caddc cseq 1nn0
      cdm cuz elnnuz sylan2br geolim2 seqex breldm isummulc2 3eqtr2rd ) AEFZBEF
      ZBGHZIJKZUBZABLMIBUCMZNMZABXGNMZLMZAOBCUAZPMZCUDZLMOAXLLMCUDXFXBXCXGEFZXG
      QUEZXHXJRXBXCXEUFZXBXCXEUGZXFIEFZXCXNUHXQIBUIUJXFXOIBUEZXFXEXSXBXCXEUKZXE
      IBIBRZIIJKXEISULYAIXDIJYAIIGHZXDIUMFQIUNKYBIRSQIUPSUOUQIURUSIBGUTVAVBVGVC
      VDXFXRXCXOXSVHUHXQXRXCVIXGQIBIBVEVFUJVJABXGVKVLXFXMXIALXCXEXMXIRXBBCVMVNV
      OXFXLACDOBDUAZPMZVPZIOVQIVSFXFVTVRXKOFZXKYEHXLRZXFDXKYDXLOYEYCXKBPWAYEWBB
      XKPWCWDWEZXFXCXKWFFXLEFYFXQXKWGBXKWHWIXFWJYEIWKZBIPMXGNMZTKYITWMFXFBCYEIX
      QXTIWFFXFWLVRXKIWNHFXFYFYGXKWOYHWPWQYIYJTWJYEIWRWSVDXPWTXA $.
      $( [26-Apr-2014] $) $( [2-Nov-2007] $)
  $}

  $( The recurring decimal 0.999..., which is defined as the infinite sum 0.9 +
     0.09 + 0.009 + ... i.e. ` 9 / 10 ^ 1 + 9 / 10 ^ 2 + 9 / 10 ^ 3 + ... ` ,
     is exactly equal to 1, according to ZF set theory.  Interestingly, about
     40% of the people responding to a poll at
     ~ http://forum.physorg.com/index.php?showtopic=13177 disagree. $)
  0.999... $p |- sum_ k e. NN ( 9 / ( 10 ^ k ) ) = 1 $=
    ( cn c9 c10 co cdiv c1 cmul cmin wcel cc0 wceq 10re recni 10pos 9re clt wbr
    cc ax-1cn cexp csu wne cn0 nnnn0 expcl sylancr a1i gt0ne0ii expne0i syl3anc
    cv nnz divrec mp3an1 syl2anc exprec oveq2d eqtr4d sumeq2i cabs cfv rereccli
    cz cle recgt0ii ltleii absidi ax-mp 1lt10 cr wb recgt1 mp2an mpbi geoisum1c
    0re eqbrtri mp3an divreci subdii mulid1i recidi oveq12i caddc addcomi df-10
    divcan2i eqtr4i subaddrii 3eqtrri eqtri redivcli subcli mulcani 9pos dividi
    divgt0ii eqtr3i 3eqtri ) BCDAULZUAEZFEZAUBBCGDFEZXAUAEZHEZAUBZCXDHEZGXDIEZF
    EZGBXCXFAXABJZXCCGXBFEZHEZXFXKXBSJZXBKUCZXCXMLZXKDSJZXAUDJXNDMNZXAUEDXAUFUG
    XKXQDKUCZXAVDJZXOXQXKXRUHZXSXKDMOUIZUHZXAUMZDXAUJUKCSJZXNXOXPCPNZCXBUNUOUPX
    KXEXLCHXKXQXSXTXEXLLYAYCYDDXAUQUKURUSUTYEXDSJXDVAVBZGQRXGXJLYFXDDMYBVCZNZYG
    XDGQKXDVERYGXDLKXDVQYHDMOVFVGXDYHVHVIGDQRZXDGQRZVJDVKJKDQRYJYKVLMODVMVNVOVR
    CXDAVPVSCDFEZYLFEXJGYLXHYLXIFCDYFXRYBVTDYLHEZDXIHEZLYLXILYMCYNCDYFXRYBWHYND
    GHEZDXDHEZIEDGIECDGXDXRTYIWAYODYPGIDXRWBDXRYBWCWDDGCXRTYFGCWEECGWEEDGCTYFWF
    WGWIWJWKWLYLXIDYLCDPMYBWMZNZGXDTYIWNXRYBWOVOWDYLYRYLYQCDPMWPOWRUIWQWSWT $.
    $( [2-Nov-2007] $)


