$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ~~> $.  $( Limit arrow. $)

  $( Extend class notation with convergence relation for limits. $)
  cli $a class ~~> $.

  ${
    $d j k x y f $.
    $( Define the limit relation for complex number sequences.  See ~ clim for
       its relational expression. $)
    df-clim $a |- ~~> = { <. f , y >. | ( y e. CC /\
        A. x e. RR+ E. j e. ZZ A. k e. ( ZZ>= ` j )
          ( ( f ` k ) e. CC /\ ( abs ` ( ( f ` k ) - y ) ) < x ) ) } $.

    $( The limit relation is a relation. $)
    climrel $p |- Rel ~~> $=
      ( vy vk vf vx vj cv cc wcel cfv cmin co cabs clt wbr wa cuz wral wrex crp
      cz cli df-clim relopabi ) AFZGHBFCFIZGHUEUDJKLIDFMNOBEFPIQETRDSQOCAUADACE
      BUBUC $.
      $( [31-Jan-2014] $) $( [28-Aug-2005] $)
  $}

  ${
    $d f j k x y A $.  $d f j k x y F $.  $d j k x ph $.
    clim.1 $e |- ( ph -> F e. V ) $.
    clim.2 $e |- ( ph -> A e. W ) $.
    clim.3 $e |- ( ( ph /\ k e. ZZ ) -> ( F ` k ) = B ) $.
    $( Express the predicate:  The limit of complex number sequence ` F ` is
       ` A ` , or ` F ` converges to ` A ` .  This means that for any real
       ` x ` , no matter how small, there always exists an integer ` j ` such
       that the absolute difference of any later complex number in the sequence
       and the limit is less than ` x ` . $)
    clim $p |- ( ph -> ( F ~~> A <-> ( A e. CC /\ A. x e. RR+ E. j e. ZZ
           A. k e. ( ZZ>= ` j ) ( B e. CC /\ ( abs ` ( B - A ) ) < x ) ) ) ) $=
      ( cc wcel cv cfv cmin wa wral cz vy vf cli wbr cabs clt cuz wrex crp wceq
      co simpr eleq1d fveq1 adantr oveq12 fveq2d breq1d anbi12d ralbidv rexbidv
      sylan df-clim brabga syl2anc eluzelz oveq1d sylan2 ralbidva anbi2d bitrd
      wb ) AGCUCUDZCMNZFOZGPZMNZVPCQUKZUEPZBOZUFUDZRZFEOZUGPZSZETUHZBUISZRZVNDM
      NZDCQUKZUEPZVTUFUDZRZFWDSZETUHZBUISZRAGHNCINVMWHVLJKUAOZMNZVOUBOZPZMNZWTW
      QQUKZUEPZVTUFUDZRZFWDSZETUHZBUISZRWHUBUAGCUCHIWSGUJZWQCUJZRZWRVNXHWGXKWQC
      MXIXJULUMXKXGWFBUIXKXFWEETXKXEWBFWDXKXAVQXDWAXKWTVPMXIWTVPUJZXJVOWSGUNZUO
      UMXKXCVSVTUFXKXBVRUEXIXLXJXBVRUJXMWTVPWQCQUPVBUQURUSUTVAUTUSBUAUBEFVCVDVE
      AWGWPVNAWFWOBUIAWEWNETAWBWMFWDVOWDNAVOTNZWBWMVLWCVOVFAXNRZVQWIWAWLXOVPDML
      UMXOVSWKVTUFXOVRWJUEXOVPDCQLVGUQURUSVHVIVAUTVJVK $.
      $( [5-Nov-2013] $) $( [28-Aug-2005] $)
  $}

  ${
    $d j k x A $.  $d j k x F $.  $d j k x V $.
    $( Closure of the limit of a sequence of complex numbers. $)
    climcl $p |- ( ( A e. V /\ F ~~> A ) -> A e. CC ) $=
      ( vk vx vj wcel cli wbr wa cc cv cfv cmin co cabs clt wral cz cvv cuz crp
      wrex simpr climrel brrelexi adantl simpl eqidd clim mpbid simpld ) ACGZBA
      HIZJZAKGZDLZBMZKGURANOPMELQIJDFLUAMRFSUCEUBRZUOUNUPUSJUMUNUDUOEAURFDBTCUN
      BTGUMBAHUEUFUGUMUNUHUOUQSGJURUIUJUKUL $.
      $( [5-Nov-2013] $) $( [28-Aug-2005] $)
  $}

  ${
    $d j k x A $.  $d j k x F $.  $d j M $.  $d j k x ph $.  $d j k Z $.
    clim2.1 $e |- Z = ( ZZ>= ` M ) $.
    clim2.2 $e |- ( ph -> M e. ZZ ) $.
    clim2.3 $e |- ( ph -> F e. V ) $.
    clim2.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = B ) $.
    ${
      clim2.5 $e |- ( ph -> A e. W ) $.
      $( Express the predicate:  The limit of complex number sequence ` F ` is
         ` A ` , or ` F ` converges to ` A ` , with more general quantifier
         restrictions than ~ clim .  (Revised by Mario Carneiro,
         31-Jan-2014.) $)
      clim2 $p |- ( ph -> ( F ~~> A <-> ( A e. CC /\ A. x e. RR+ E. j e. Z
             A. k e. ( ZZ>= ` j ) ( B e. CC /\ ( abs ` ( B - A ) ) < x ) ) ) )
          $=
        ( cc wcel cfv wa cli wbr cv cmin co cabs clt cuz wral cz wrex crp eqidd
        clim uztrn2 eleq1d oveq1d fveq2d breq1d anbi12d sylan2 anassrs ralbidva
        wb rexbidva rexuz3 syl bitr3d ralbidv anbi2d bitr4d ) AGCUAUBCQRZFUCZGS
        ZQRZVNCUDUEZUFSZBUCZUGUBZTZFEUCZUHSZUIZEUJUKZBULUIZTVLDQRZDCUDUEZUFSZVR
        UGUBZTZFWBUIZEKUKZBULUIZTABCVNEFGIJNPAVMUJRTVNUMUNAWMWEVLAWLWDBULAWCEKU
        KZWLWDAWCWKEKAWAKRZTVTWJFWBAWOVMWBRZVTWJVDZWOWPTAVMKRZWQHVMWAKLUOAWRTZV
        OWFVSWIWSVNDQOUPWSVQWHVRUGWSVPWGUFWSVNDCUDOUQURUSUTVAVBVCVEAHUJRWNWDVDM
        VTEFHKLVFVGVHVIVJVK $.
        $( [31-Jan-2014] $) $( [6-Jan-2007] $)
    $}

    clim2c.5 $e |- ( ph -> A e. CC ) $.
    clim2c.6 $e |- ( ( ph /\ k e. Z ) -> B e. CC ) $.
    $( Express the predicate ` F ` converges to ` A ` .  (Revised by Mario
       Carneiro, 31-Jan-2014.) $)
    clim2c $p |- ( ph -> ( F ~~> A <->
      A. x e. RR+ E. j e. Z A. k e. ( ZZ>= ` j ) ( abs ` ( B - A ) ) < x ) ) $=
      ( cc wcel wa wral cmin cabs cfv clt wbr cuz wrex crp cli biantrurd uztrn2
      co cv wb sylan2 anassrs ralbidva rexbidva ralbidv clim2 3bitr4rd ) ADQRZD
      CUAULUBUCBUMUDUEZSZFEUMZUFUCZTZEJUGZBUHTZCQRZVISVCFVFTZEJUGZBUHTGCUIUEAVJ
      VIOUJAVLVHBUHAVKVGEJAVEJRZSVCVDFVFAVMFUMZVFRZVCVDUNZVMVOSAVNJRZVPHVNVEJKU
      KAVQSVBVCPUJUOUPUQURUSABCDEFGHIQJKLMNOUTVA $.
      $( [31-Jan-2014] $) $( [24-Feb-2008] $)
  $}

  ${
    $d j k x F $.  $d j M $.  $d j k x ph $.  $d j k Z $.
    clim0.1 $e |- Z = ( ZZ>= ` M ) $.
    clim0.2 $e |- ( ph -> M e. ZZ ) $.
    clim0.3 $e |- ( ph -> F e. V ) $.
    clim0.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = B ) $.
    $( Express the predicate ` F ` converges to ` 0 ` .  (Revised by Mario
       Carneiro, 31-Jan-2014.) $)
    clim0 $p |- ( ph -> ( F ~~> 0 <-> A. x e. RR+ E. j e. Z
                  A. k e. ( ZZ>= ` j ) ( B e. CC /\ ( abs ` B ) < x ) ) ) $=
      ( cc0 wbr cc cabs cfv clt wral cli wcel cmin co cv cuz wrex crp 0cn clim2
      wa a1i biantrur subid1 fveq2d breq1d pm5.32i ralbii rexbii bitr3i syl6bb
      ) AFNUAONPUBZCPUBZCNUCUDZQRZBUEZSOZUKZEDUEUFRZTZDIUGZBUHTZUKZVCCQRZVFSOZU
      KZEVITZDIUGZBUHTZABNCDEFGHPIJKLMVBAUIULUJVMVLVSVBVLUIUMVKVRBUHVJVQDIVHVPE
      VIVCVGVOVCVEVNVFSVCVDCQCUNUOUPUQURUSURUTVA $.
      $( [31-Jan-2014] $) $( [24-Feb-2008] $)

    clim0c.6 $e |- ( ( ph /\ k e. Z ) -> B e. CC ) $.
    $( Express the predicate ` F ` converges to ` 0 ` .  (Revised by Mario
       Carneiro, 31-Jan-2014.) $)
    clim0c $p |- ( ph -> ( F ~~> 0 <->
              A. x e. RR+ E. j e. Z A. k e. ( ZZ>= ` j ) ( abs ` B ) < x ) ) $=
      ( cc0 wbr cabs cfv wral wcel cli cmin co cv clt cuz crp cc 0cn a1i clim2c
      wrex wa wb uztrn2 wceq subid1 syl fveq2d breq1d anassrs ralbidva rexbidva
      sylan2 ralbidv bitrd ) AFOUAPCOUBUCZQRZBUDZUEPZEDUDZUFRZSZDIULZBUGSCQRZVI
      UEPZEVLSZDIULZBUGSABOCDEFGHIJKLMOUHTAUIUJNUKAVNVRBUGAVMVQDIAVKITZUMVJVPEV
      LAVSEUDZVLTZVJVPUNZVSWAUMAVTITZWBGVTVKIJUOAWCUMZVHVOVIUEWDVGCQWDCUHTVGCUP
      NCUQURUSUTVDVAVBVCVEVF $.
      $( [31-Jan-2014] $) $( [24-Feb-2008] $)
  $}

  ${
    $d j k x A $.  $d j k x C $.  $d j k x F $.  $d j k x ph $.  $d j k x Z $.
    $d x B $.  $d j M $.
    climi.1 $e |- Z = ( ZZ>= ` M ) $.
    climi.2 $e |- ( ph -> M e. ZZ ) $.
    climi.3 $e |- ( ph -> C e. RR+ ) $.
    climi.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = B ) $.
    ${
      climi.5 $e |- ( ph -> F ~~> A ) $.
      climi.6 $e |- ( ph -> A e. V ) $.
      $( Convergence of a sequence of complex numbers.  (Revised by Mario
         Carneiro, 31-Jan-2014.) $)
      climi $p |- ( ph -> E. j e. Z A. k e. ( ZZ>= ` j )
                    ( B e. CC /\ ( abs ` ( B - A ) ) < C ) ) $=
        ( vx crp wcel clt cc cmin co cabs cfv wbr cuz wral wrex cli cvv climrel
        cv wa brrelexi syl clim2 mpbid simprd wceq breq2 anbi2d rexralbidv sylc
        rcla4v ) ADRSCUASZCBUBUCUDUEZQUMZTUFZUNZFEUMUGUEZUHEJUIZQRUHZVFVGDTUFZU
        NZFVKUHEJUIZMABUASZVMAGBUJUFZVQVMUNOAQBCEFGHUKIJKLAVRGUKSOGBUJULUOUPNPU
        QURUSVLVPQDRVHDUTZVJVOEFJVKVSVIVNVFVHDVGTVAVBVCVEVD $.
        $( [31-Jan-2014] $) $( [11-Jan-2007] $)

      $( Convergence of a sequence of complex numbers.  (Revised by Mario
         Carneiro, 31-Jan-2014.) $)
      climi2 $p |- ( ph -> E. j e. Z A. k e. ( ZZ>= ` j )
                     ( abs ` ( B - A ) ) < C ) $=
        ( cc cfv wral wrex wcel cmin co cabs clt wbr wa cuz climi ralimi reximi
        cv simpr syl ) ACQUAZCBUBUCUDRDUEUFZUGZFEULUHRZSZEJTUPFURSZEJTABCDEFGHI
        JKLMNOPUIUSUTEJUQUPFURUOUPUMUJUKUN $.
        $( [31-Jan-2014] $) $( [11-Jan-2007] $)
    $}

    climi0.5 $e |- ( ph -> F ~~> 0 ) $.
    $( Convergence of a sequence of complex numbers to zero.  (Revised by Mario
       Carneiro, 31-Jan-2014.) $)
    climi0 $p |- ( ph -> E. j e. Z A. k e. ( ZZ>= ` j ) ( abs ` B ) < C ) $=
      ( cc wcel cc0 cabs cfv clt wbr cmin co wa cv cuz wral wrex 0cn a1i subid1
      climi fveq2d breq1d biimpa ralimi reximi syl ) ABNOZBPUAUBZQRZCSTZUCZEDUD
      UERZUFZDHUGBQRZCSTZEVCUFZDHUGAPBCDEFGNHIJKLMPNOAUHUIUKVDVGDHVBVFEVCURVAVF
      URUTVECSURUSBQBUJULUMUNUOUPUQ $.
      $( [31-Jan-2014] $) $( [11-Jan-2007] $)
  $}

  ${
    $d j k x A $.  $d j k x F $.  $d j M $.  $d j k x ph $.  $d j k Z $.
    climconst.1 $e |- Z = ( ZZ>= ` M ) $.
    climconst.2 $e |- ( ph -> M e. ZZ ) $.
    climconst.3 $e |- ( ph -> F e. V ) $.
    climconst.4 $e |- ( ph -> A e. CC ) $.
    climconst.5 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = A ) $.
    $( An (eventually) constant sequence converges to its value.  (Revised by
       Mario Carneiro, 31-Jan-2014.) $)
    climconst $p |- ( ph -> F ~~> A ) $=
      ( vx vj wbr cabs cfv cv wcel cc0 cli cmin co clt cuz wral wrex wa cz uzid
      crp syl syl6eleqr adantr wceq cc subid fveq2d syl6eq rpgt0 adantl eqbrtrd
      abs0 a1d ralrimiv syl6eqr raleqdv rcla4ev syl2anc ralrimiva clim2c mpbird
      fveq2 ) ADBUAOBBUBUCZPQZMRZUDOZCNRZUEQZUFZNGUGZMUKUFAWAMUKAVPUKSZUHZEGSZV
      QCGUFZWAAWDWBAEEUEQZGAEUISEWFSIEUJULHUMUNWCVQCGWCVQCRGSZWCVOTVPUDAVOTUOWB
      AVOTPQTAVNTPABUPSZVNTUOKBUQULURVCUSUNWBTVPUDOAVPUTVAVBVDVEVTWENEGVREUOZVQ
      CVSGWIVSWFGVREUEVMHVFVGVHVIVJAMBBNCDEFGHIJLKAWHWGKUNVKVL $.
      $( [31-Jan-2014] $) $( [28-Aug-2005] $)
  $}

  ${
    $d k A $.  $d k M $.  $d k Z $.
    climconst2.1 $e |- ( ZZ>= ` M ) C_ Z $.
    climconst2.2 $e |- Z e. _V $.
    $( A constant sequence converges to its value.  (Revised by Mario Carneiro,
       31-Jan-2014.) $)
    climconst2 $p |- ( ( A e. CC /\ M e. ZZ ) -> ( Z X. { A } ) ~~> A ) $=
      ( vk cc wcel cz wa csn cxp cvv cuz cfv eqid simpr snex xpex a1i fvconst2g
      simpl cv wceq sseli syl2an climconst ) AGHZBIHZJZAFCAKZLZBMBNOZUMPUHUIQUL
      MHUJCUKEARSTUHUIUBZUJUHFUCZCHUOULOAUDUOUMHUNUMCUODUECAUOGUAUFUG $.
      $( [31-Jan-2014] $) $( [6-Feb-2008] $)
  $}

  $( The zero sequence converges to zero. $)
  climz $p |- ( ZZ X. { 0 } ) ~~> 0 $=
    ( cc0 cc wcel cz csn cxp cli wbr 0cn 0z uzssz zex climconst2 mp2an ) ABCADC
    DAEFAGHIJAADAKLMN $.
    $( [31-Jan-2014] $) $( [2-Oct-1999] $)

  ${
    $d j k x A $.  $d j k x B $.  $d j k x F $.
    climuni.1 $e |- A e. _V $.
    climuni.2 $e |- B e. _V $.
    $( An infinite sequence of complex numbers converges to at most one limit.
       (The proof was shortened by Mario Carneiro, 31-Jan-2014.) $)
    climuni $p |- ( ( F ~~> A /\ F ~~> B ) -> A = B ) $=
      ( vk vj wbr wa c1 wcel cfv cc cmin co clt cn cvv syl2anc wi cli cz 1z wne
      wceq wn w3a cv cabs c2 cdiv cuz wral wrex nnuz a1i crp cc0 climcl sylancr
      simp1 simp2 subcl simp3 wb subeq0 necon3bid mpbird absrpcl rphalfcl eqidd
      syl climi rexanuz2 sylanbrc c0 nnz uzid ne0i 3syl r19.2z ex simpll abssub
      simpr breq1d simplr adantr abscl abs3lem syl22anc ltnr pm2.21d syld exp3a
      cr sylbid impr adantld expimpd rexlimdva sylan9r mpd 3expia necon4ad mpi
      ) CAUAHZCBUAHZIZJUBKZABUEZUCXIXJABXGXHABUDZXJUFZXGXHXLUGZFUHZCLZMKZXPANOU
      ILZABNOZUILZUJUKOZPHZIZXQXPBNOUILYAPHZIZIZFGUHZULLZUMZGQUNZXMXNYCFYHUMGQU
      NYEFYHUMGQUNYJXNAXPYAGFCJRQUOXJXNUCUPZXNXTUQKZYAUQKXNXSMKZXSURUDZYLXNAMKZ
      BMKZYMXNARKZXGYODXGXHXLVAZACRUSUTZXNBRKZXHYPEXGXHXLVBZBCRUSUTZABVCZSXNYNX
      LXGXHXLVDXNXSURABXNYOYPXSURUEXKVEYSUUBABVFSVGVHXSVISXTVJVLZXNXOQKIXPVKZYR
      YQXNDUPVMXNBXPYAGFCJRQUOYKUUDUUEUUAYTXNEUPVMYCYEGFJQUOVNVOXNYOYPYJXMTYSUU
      BYOYPIZYIXMGQYGQKZYIYFFYHUNZUUFXMUUGYHVPUDZYIUUHTUUGYGUBKYGYHKUUIYGVQYGVR
      YHYGVSVTUUIYIUUHYFFYHWAWBVLUUFYFXMFYHUUFYFXMTXOYHKUUFYCYEXMUUFYCIYDXMXQUU
      FXQYBYDXMTZUUFXQIZYBAXPNOUILZYAPHZUUJUUKXRUULYAPUUKXQYOXRUULUEUUFXQWEZYOY
      PXQWCZXPAWDSWFUUKUUMYDXMUUKUUMYDIZXTXTPHZXMUUKYOYPXQXTWPKZUUPUUQTUUOYOYPX
      QWGUUNUUKYMUURUUFYMXQUUCWHXSWIVLZABXPXTWJWKUUKUUQXMUUKUURUUQUFUUSXTWLVLWM
      WNWOWQWRWSWTWHXAXBXASXCXDXEXF $.
      $( [31-Jan-2014] $) $( [2-Oct-1999] $)
  $}

  ${
    $d x y z $.
    $( The limit relation is function-like, and with range the complex
       numbers.  (Contributed by Mario Carneiro, 31-Jan-2014.) $)
    fclim $p |- ~~> : dom ~~> --> CC $=
      ( vx vy vz cli cdm cc wf wfn crn wss df-f wfun cv wbr wal ax-gen mpbir2an
      vex wcel cvv wrel wa wceq wi dffun2 climrel climuni funfn mpbi wex climcl
      elrn mpan exlimiv sylbi ssriv ) DEZFDGDUQHZDIZFJUQFDKDLZURUTDUAAMZBMZDNZV
      ACMZDNUBVBVDUCUDZCOZBOZAOABCDUEUFVGAVFBVECVBVDVABRZCRUGPPPQDUHUIBUSFVBUSS
      VCAUJVBFSZAVBDVHULVCVIAVBTSVCVIVHVBVATUKUMUNUOUPQ $.
      $( [31-Jan-2014] $)
  $}

  $( Two ways to express that a function has a limit.  (The expression
     ` ( ~~> `` F ) ` is sometimes useful as a shorthand for "the unique limit
     of the function ` F ` ").  (Contributed by Mario Carneiro,
     18-Mar-2014.) $)
  climdm $p |- ( F e. dom ~~> <-> F ~~> ( ~~> ` F ) ) $=
    ( cli cdm cc wf wfun wcel cfv wbr wb fclim ffun funfvbrb mp2b ) BCZDBEBFAOG
    AABHBIJKODBLABMN $.
    $( [18-Mar-2014] $)

  ${
    $d y A $.  $d x y F $.
    climreu.1 $e |- A e. _V $.
    $( An infinite sequence of complex numbers converges to at most one
       limit. $)
    climeu $p |- ( F ~~> A -> E! x F ~~> x ) $=
      ( vy cli wbr cv wex wa wceq wal weu breq2 cla4ev vex climuni gen2 ax-17
      wi jctir cbveu eu4 bitri sylibr ) CBFGZCEHZFGZEIZUHCAHZFGZJUGUJKTZALELZJZ
      UKAMZUFUIUMUHUFEBDUGBCFNOULEAUGUJCEPAPQRUAUOUHEMUNUKUHAEUKESUHASUJUGCFNUB
      UHUKEAUGUJCFNUCUDUE $.
      $( [25-Dec-2005] $)

    $( An infinite sequence of complex numbers converges to at most one
       limit. $)
    climreu $p |- ( F ~~> A -> E! x e. CC F ~~> x ) $=
      ( cli wbr cv weu cc wreu climeu wcel cvv vex climcl pm4.71ri eubii df-reu
      wa mpan bitr4i sylib ) CBEFCAGZEFZAHZUDAIJZABCDKUEUCILZUDSZAHUFUDUHAUDUGU
      CMLUDUGANUCCMOTPQUDAIRUAUB $.
      $( [25-Dec-2005] $)
  $}

  ${
    $d F x y $.
    $( An infinite sequence of complex numbers converges to at most one limit.
       (Contributed by Mario Carneiro, 13-Jul-2013.) $)
    climmo $p |- E* x F ~~> x $=
      ( vy cv cli wbr wmo wex weu wi breq2 cbvexv vex exlimiv sylbi df-mo mpbir
      climeu ) BADZEFZAGTAHZTAIZJUABCDZEFZCHUBTUDACSUCBEKLUDUBCAUCBCMRNOTAPQ $.
      $( [13-Jul-2013] $)
  $}

  ${
    $d k x y A $.  $d k x y F $.  $d k x y G $.  $d k x y ph $.  $d k y Z $.
    $d y M $.
    climeq.1 $e |- Z = ( ZZ>= ` M ) $.
    climeq.2 $e |- ( ph -> F e. V ) $.
    climeq.3 $e |- ( ph -> G e. W ) $.
    climeq.4 $e |- ( ph -> A e. X ) $.
    climeq.5 $e |- ( ph -> M e. ZZ ) $.
    climeq.6 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = ( G ` k ) ) $.
    $( Two functions that are eventually equal to one another have the same
       limit. $)
    climeq $p |- ( ph -> ( F ~~> A <-> G ~~> A ) ) $=
      ( vx vy wbr wcel cli cc cv cfv cmin co cabs clt cuz wral wrex clim2 eqidd
      wa crp bitr4d ) ADBUASBUBTCUCZEUDZUBTURBUEUFUGUDQUCUHSUNCRUCUIUDUJRJUKQUO
      UJUNEBUASAQBURRCDFGIJKOLPNULAQBURRCEFHIJKOMAUQJTUNURUMNULUP $.
      $( [31-Jan-2014] $) $( [5-Nov-2013] $)
  $}

  ${
    $d j k m y A $.  $d j k m x F $.  $d j m x y G $.  $d j m M $.  $d m V $.
    $d j k y ph $.  $d m W $.  $d j k m x Z $.
    2clim.1 $e |- Z = ( ZZ>= ` M ) $.
    ${
      climmpt.2 $e |- G = ( k e. Z |-> ( F ` k ) ) $.
      $( Exhibit a function ` G ` with the same convergence properties as the
         not-quite-function ` F ` .  (Contributed by Mario Carneiro,
         31-Jan-2014.) $)
      climmpt $p |- ( ( M e. ZZ /\ F e. V /\ A e. W ) ->
                      ( F ~~> A <-> G ~~> A ) ) $=
        ( vm cz wcel w3a cvv cv cfv cuz fvex eqeltri simp2 cmpt mptex a1i simp3
        simp1 wceq fveq2 fvmpt eqcomd adantl climeq ) ELMZCFMZAGMZNZAKCDEFOGHIU
        MUNUOUADOMUPDBHBPZCQZUBOJBHURHERQOIERSTUCTUDUMUNUOUEUMUNUOUFKPZHMZUSCQZ
        USDQZUGUPUTVBVABUSURVAHDUQUSCUHJUSCSUIUJUKUL $.
        $( [31-Jan-2014] $)
    $}
    $d k G $.
    2clim.2 $e |- ( ph -> M e. ZZ ) $.
    2clim.3 $e |- ( ph -> G e. V ) $.
    2clim.4 $e |- ( ph -> A e. W ) $.
    2clim.5 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) e. CC ) $.
    2clim.6 $e |- ( ph -> A. x e. RR+ E. j e. Z A. k e. ( ZZ>= ` j )
                    ( abs ` ( ( F ` k ) - ( G ` k ) ) ) < x ) $.
    2clim.7 $e |- ( ph -> F ~~> A ) $.
    $( If two sequences converge to each other, they converge to the same
       limit.  (The proof was shortened by Mario Carneiro, 31-Jan-2014.) $)
    2clim $p |- ( ph -> G ~~> A ) $=
      ( wcel wa vy cli wbr cv cfv cmin co cabs clt cuz wral wrex crp c2 cdiv cc
      rphalfcl breq2 rexralbidv rcla4cva syl2an cz adantr adantl eqidd rexanuz2
      wceq sylanbrc wi uztrn2 an12 simprr ad2ant2r abssub syl2anc breq1d anbi1d
      climi cr climcl ad2antrr ad2antlr abs3lem syl22anc sylbid anassrs expimpd
      rpre syl5bi sylan2 ralimdva reximdva mpd ralrimiva clim2c mpbird ) AGCUBU
      CEUDZGUEZCUFUGUHUEUAUDZUIUCZEDUDZUJUEZUKZDKULZUAUMUKAXDUAUMAWSUMSZTZWQFUE
      ZWRUFUGUHUEZWSUNUOUGZUIUCZXGUPSZXGCUFUGUHUEXIUIUCZTZTZEXBUKZDKULZXDXFXJEX
      BUKDKULZXMEXBUKDKULXPAXHBUDZUIUCZEXBUKDKULZBUMUKXIUMSZXQXEQWSUQZXTXQBXIUM
      XRXIVGXSXJDEKXBXRXIXHUIURUSUTVAXFCXGXIDEFHJKLAHVBSXEMVCXEYAAYBVDXFWQKSZTZ
      XGVEAFCUBUCZXERVCACJSZXEOVCVRXJXMDEHKLVFVHXFXOXCDKXFXAKSZTXNWTEXBXFYGWQXB
      SZXNWTVIZYGYHTXFYCYIHWQXAKLVJXNXKXJXLTZTYDWTXJXKXLVKYDXKYJWTXFYCXKYJWTVIX
      FYCXKTZTZYJWRXGUFUGUHUEZXIUIUCZXLTZWTYLXJYNXLYLXHYMXIUIYLXKWRUPSZXHYMVGXF
      YCXKVLZAYCYPXEXKPVMZXGWRVNVOVPVQYLYPCUPSZXKWSVSSZYOWTVIYRAYSXEYKAYFYEYSOR
      CFJVTVOZWAYQXEYTAYKWSWHWBWRCXGWSWCWDWEWFWGWIWJWFWKWLWMWNAUACWRDEGHIKLMNAY
      CTWRVEUUAPWOWP $.
      $( [31-Jan-2014] $) $( [24-Dec-2005] $)
  $}

  ${
    $d f k m n x A $.  $d f k m n x F $.  $d f k m n x M $.  $d k m n x V $.
    $d f k W $.
    ${
      climshft.1 $e |- F e. _V $.
      $( A shifted function converges if the original function converges. $)
      climshftlem $p |- ( ( M e. ZZ /\ A e. V ) ->
                          ( F ~~> A -> ( F shift M ) ~~> A ) ) $=
        ( vm vx vk vn cz wcel wa cc cfv cmin co cabs clt wbr wral cuz wrex cshi
        cv crp cli caddc zaddcl ancoms eluzsub 3com12 3expa fveq2 eleq1d oveq1d
        wi wceq fveq2d breq1d anbi12d rcla4v syl wb zcn eluzelz shftval adantlr
        syl2an sylibrd ralrimdva raleqdv rcla4ev ex rexlimdva anim2d adantr cvv
        sylsyld ralimdv a1i simpr eqidd clim ovex 3imtr4d ) CJKZADKZLZAMKZFUDZB
        NZMKZWKAOPZQNZGUDZRSZLZFHUDZUANZTZHJUBZGUETZLZWIIUDZBCUCPZNZMKZXFAOPZQN
        ZWORSZLZIWJUANZTZFJUBZGUETZLZBAUFSXEAUFSWFXCXPUPWGWFXBXOWIWFXAXNGUEWFWT
        XNHJWFWRJKZLZWRCUGPZJKZWTXKIXSUANZTZXNXQWFXTWRCUHUIXRWTXKIYAXRXDYAKZLZW
        TXDCOPZBNZMKZYFAOPZQNZWORSZLZXKYDYEWSKZWTYKUPWFXQYCYLXQWFYCYLCWRXDUJUKU
        LWQYKFYEWSWJYEUQZWLYGWPYJYMWKYFMWJYEBUMZUNYMWNYIWORYMWMYHQYMWKYFAOYNUOU
        RUSUTVAVBWFYCXKYKVCZXQWFCMKZXDMKZYOYCCVDYCXDJKZYQXSXDVEXDVDVBYPYQLZXGYG
        XJYJYSXFYFMCXDBEVFZUNYSXIYIWORYSXHYHQYSXFYFAOYTUOURUSUTVHVGVIVJXTYBXNXM
        YBFXSJWJXSUQXKIXLYAWJXSUAUMVKVLVMVRVNVSVOVPWHGAWKHFBVQDBVQKWHEVTWFWGWAZ
        WHWJJKLWKWBWCWHGAXFFIXEVQDXEVQKWHBCUCWDVTUUAWHYRLXFWBWCWE $.
        $( [5-Nov-2013] $)
    $}

    $( A function restricted to upper integers converges iff the original
       function converges.  (Contributed by Mario Carneiro, 13-Jul-2013.) $)
    climres $p |- ( ( M e. ZZ /\ F e. V /\ A e. W ) ->
                        ( ( F |` ( ZZ>= ` M ) ) ~~> A <-> F ~~> A ) ) $=
      ( vk cz wcel w3a cuz cfv cres cvv eqid simp2 resexg syl simp3 simp1 cv
      wceq fvres adantl climeq ) CGHZBDHZAEHZIZAFBCJKZLZBCMDEUIUINUHUFUJMHUEUFU
      GOZBUIDPQUKUEUFUGRUEUFUGSFTZUIHULUJKULBKUAUHULUIBUBUCUD $.
      $( [31-Jan-2014] $) $( [13-Jul-2013] $)

    $( A shifted function converges iff the original function converges. $)
    climshft $p |- ( ( M e. ZZ /\ F e. V /\ A e. W ) ->
                      ( ( F shift M ) ~~> A <-> F ~~> A ) ) $=
      ( vf vk wcel cz cshi co cli wbr wb cv wi wceq ovex cvv cfv wa oveq1 breq1
      breq1d bibi12d imbi2d cneg climshftlem sylan cuz eqid a1i vex simpr simpl
      znegcl cc zcn eluzelz shftcan1 syl2an adantlr climeq sylibd impbid vtoclg
      syl 3impib 3com12 ) BDHZCIHZAEHZBCJKZALMZBALMZNZVJVKVLVPVKVLUAZFOZCJKZALM
      ZVRALMZNZPVQVPPFBDVRBQZWBVPVQWCVTVNWAVOWCVSVMALVRBCJUBUDVRBALUCUEUFVQVTWA
      VQVTVSCUGZJKZALMZWAVKWDIHVLVTWFPCUPAVSWDEVRCJRUHUIVQAGWEVRCSSECUJTZWGUKWE
      SHVQVSWDJRULVRSHVQFUMZULVKVLUNVKVLUOVKGOZWGHZWIWETWIVRTQZVLVKCUQHWIUQHZWK
      WJCURWJWIIHWLCWIUSWIURVGCWIVRWHUTVAVBVCVDAVRCEWHUHVEVFVHVI $.
      $( [31-Jan-2014] $) $( [16-Aug-2005] $)
  $}

  ${
    $d k M $.
    $( The zero series converges to zero.  (Contributed by Paul Chapman,
       9-Feb-2007.)  (The proof was shortened by Mario Carneiro,
       31-Jan-2014.) $)
    serclim0 $p |- ( M e. ZZ ->
                      seq M ( + , ( ( ZZ>= ` M ) X. { 0 } ) ) ~~> 0 ) $=
      ( cz wcel caddc cuz cfv cc0 csn cxp cseq cli eqid ser0f wbr 0cn ssid fvex
      cc climconst2 mpan eqbrtrd ) ABCZDAEFZGHIZAJUDGKAUCUCLMGRCUBUDGKNOGAUCUCP
      AEQSTUA $.
      $( [31-Jan-2014] $) $( [9-Feb-2008] $)
  $}

  ${
    $d j k F $.  $d k G $.  $d k K $.  $d j k M $.  $d j k x ph $.  $d j k Z $.
    $d j k x A $.
    climshft2.1 $e |- Z = ( ZZ>= ` M ) $.
    climshft2.2 $e |- ( ph -> M e. ZZ ) $.
    ${
      climshft2.3 $e |- ( ph -> K e. ZZ ) $.
      climshft2.4 $e |- ( ph -> A e. V ) $.
      climshft2.5 $e |- ( ph -> F e. W ) $.
      climshft2.6 $e |- ( ph -> G e. X ) $.
      climshft2.7 $e |- ( ( ph /\ k e. Z ) ->
                          ( G ` ( k + K ) ) = ( F ` k ) ) $.
      $( A shifted function converges iff the original function converges.
         (Contributed by Paul Chapman, 19-Nov-2007.)  (Revised by Mario
         Carneiro, 6-Feb-2014.) $)
      climshft2 $p |- ( ph -> ( F ~~> A <-> G ~~> A ) ) $=
        ( wcel cfv cneg cshi co cli wbr cvv ovex a1i cv wa caddc cid cc wceq cz
        zcn syl eluzelz eleq2s fvex shftval4 syl2an adantr oveq1d fveq1d addcom
        cuz fvi fveq12d 3eqtr3d eqtrd climeq wb znegcl climshft syl3anc bitr3d
        ) AEFUAZUBUCZBUDUEZDBUDUEEBUDUEZABCVSDGUFIHKLVSUFSAEVRUBUGUHPOMACUIZKSZ
        UJZWBVSTZWBFUKUCZETZWBDTWDWBEULTZVRUBUCZTZFWBUKUCZWHTZWEWGAFUMSZWBUMSZW
        JWLUNWCAFUOSZWMNFUPUQZWCWBUOSZWNWQWBGVGTKGWBURLUSWBUPUQZFWBWHEULUTVAVBW
        DWBWIVSWDWHEVRUBAWHEUNZWCAEJSZWSQEJVHUQVCZVDVEWDWKWFWHEXAAWMWNWKWFUNWCW
        PWRFWBVFVBVIVJRVKVLAVRUOSZWTBHSVTWAVMAWOXBNFVNUQQOBEVRJHVOVPVQ $.
        $( [6-Feb-2014] $) $( [21-Nov-2007] $)
    $}

    climrecl.3 $e |- ( ph -> F ~~> A ) $.
    climrecl.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. RR ) $.
    ${
      climrecl.5 $e |- ( ph -> A e. V ) $.
      $( The limit of a convergent real sequence is real.  Corollary 12-2.5 of
         [Gleason] p. 172.  (The proof was shortened by Mario Carneiro,
         31-Jan-2014.) $)
      climrecl $p |- ( ph -> A e. RR ) $=
        ( vj cr wcel cfv cc0 wceq cabs syl cim wne wa cv cmin clt wbr wrex wral
        co cuz cz adantr crp cli climcl syl2anc imcl recnd absrpcl sylan climi2
        cc eqidd r19.2uz cle wn cneg adantlr ad2antrr imsub reim0 oveq1d df-neg
        eqtrd syl6eqr fveq2d absneg subcl absimle eqbrtrrd wb abscl lenlt mpbid
        pm2.21d rexlimdva mpd ex necon1bd pm2.18d reim0b mpbird ) ABNOZBUAPZQRZ
        AWPAWPWOQAWOQUBZWPAWQUCZCUDZDPZBUEUJZSPZWOSPZUFUGZCGUHZWPWRXDCMUDUKPUIM
        GUHXEWRBWTXCMCDEFGHAEULOWQIUMAWOVCOZWQXCUNOAWOABVCOZWONOABFOZDBUOUGZXGL
        JBDFUPUQZBURTUSZWOUTVAWRWSGOZUCZWTVDAXIWQJUMAXHWQLUMVBXDMCEGHVETWRXDWPC
        GXMXDWPXMXCXBVFUGZXDVGZXMXAUAPZSPZXCXBVFXMXQWOVHZSPZXCXMXPXRSXMXPQWOUEU
        JZXRXMXPWTUAPZWOUEUJZXTXMWTVCOZXGXPYBRXMWTAXLWTNOZWQKVIZUSZAXGWQXLXJVJZ
        WTBVKUQXMYAQWOUEXMYDYAQRYEWTVLTVMVOWOVNVPVQXMXFXSXCRAXFWQXLXKVJWOVRTVOX
        MXAVCOZXQXBVFUGXMYCXGYHYFYGWTBVSUQZXAVTTWAXMXCNOZXBNOZXNXOWBAYJWQXLAXFY
        JXKWOWCTVJXMYHYKYIXAWCTXCXBWDUQWEWFWGWHWIWJWKAXGWNWPWBXJBWLTWM $.
        $( [31-Jan-2014] $) $( [10-Sep-2005] $)
    $}

    climge0.5 $e |- ( ( ph /\ k e. Z ) -> 0 <_ ( F ` k ) ) $.
    $( A nonnegative sequence converges to a nonnegative number.  (The proof
       was shortened by Mario Carneiro, 31-Jan-2014.) $)
    climge0 $p |- ( ph -> 0 <_ A ) $=
      ( vx vj wcel cc0 cle wbr wa clt cr cvv cv caddc co crp wral cfv cmin cabs
      wrex cuz cz adantr simpr eqidd cli ad2antrr simplr climi2 r19.2uz syl 0re
      adantlr ad2ant2r climrecl rpre readdcl syl2an simprr wb ad2antlr absdiflt
      a1i syl3anc mpbid simprd lelttrd expr rexlimdva wi ltle sylancr ralrimiva
      mpd alrple mpbird ex wn leidi brprc mpbiri pm2.61d1 ) ABUANZOBPQZAWMWNAWM
      RZWNOBLUBZUCUDZPQZLUEUFZWOWRLUEWOWPUENZRZOWQSQZWRXACUBZDUGZBUHUDUIUGWPSQZ
      CFUJZXBXAXECMUBUKUGUFMFUJXFXABXDWPMCDEUAFGWOEULNZWTAXGWMHUMZUMWOWTUNXAXCF
      NZRXDUOADBUPQZWMWTIUQAWMWTURUSXEMCEFGUTVAXAXEXBCFXAXIXEXBXAXIXERZRZOXDWQO
      TNZXLVBVMWOXIXDTNZWTXEAXIXNWMJVCZVDZXAWQTNZXKWOBTNZWPTNZXQWTWOBCDEUAFGXHA
      XJWMIUMXOAWMUNVEZWPVFZBWPVGVHZUMWOXIOXDPQZWTXEAXIYCWMKVCVDXLBWPUHUDXDSQZX
      DWQSQZXLXEYDYERZXAXIXEVIXLXNXRXSXEYFVJXPWOXRWTXKXTUQWTXSWOXKYAVKXDBWPVLVN
      VOVPVQVRVSWDXAXMXQXBWRVTVBYBOWQWAWBWDWCWOXMXRWNWSVJVBXTLOBWEWBWFWGWMWHWNO
      OPQOVBWIOBPWJWKWL $.
      $( [12-Jun-2014] $) $( [11-Sep-2005] $)
  $}

  ${
    $d j k x F $.  $d j k x G $.  $d j M $.  $d j k x ph $.  $d j k Z $.
    climabs0.1 $e |- Z = ( ZZ>= ` M ) $.
    climabs0.2 $e |- ( ph -> M e. ZZ ) $.
    climabs0.3 $e |- ( ph -> F e. V ) $.
    climabs0.4 $e |- ( ph -> G e. W ) $.
    climabs0.5 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
    climabs0.6 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) = ( abs ` ( F ` k ) ) ) $.
    $( Convergence to zero of the absolute value is equivalent to convergence
       to zero.  (Revised by Mario Carneiro, 31-Jan-2014.) $)
    climabs0 $p |- ( ph -> ( F ~~> 0 <-> G ~~> 0 ) ) $=
      ( vx vj cfv wbr wral wcel cv cabs clt cuz wrex crp cc0 cli wa uztrn2 wceq
      wb cc absidm syl breq1d sylan2 anassrs ralbidva rexbidva ralbidv cr abscl
      recnd clim0c eqidd 3bitr4rd ) ABUAZCQZUBQZUBQZOUAZUCRZBPUAZUDQZSZPHUEZOUF
      SVJVLUCRZBVOSZPHUEZOUFSDUGUHRCUGUHRAVQVTOUFAVPVSPHAVNHTZUIVMVRBVOAWAVHVOT
      ZVMVRULZWAWBUIAVHHTZWCEVHVNHIUJAWDUIZVKVJVLUCWEVIUMTZVKVJUKMVIUNUOUPUQURU
      SUTVAAOVJPBDEGHIJLNWEVJWEWFVJVBTMVIVCUOVDVEAOVIPBCEFHIJKWEVIVFMVEVG $.
      $( [31-Jan-2014] $) $( [8-Jul-2008] $)
  $}

  ${
    $d j k x y z A $.  $d j k z B $.  $d j k y z G $.  $d j k x H $.  $d j M $.
    $d j k x y z F $.  $d j k x y z ph $.  $d j k y Z $.
    climcn1.1 $e |- Z = ( ZZ>= ` M ) $.
    climcn1.2 $e |- ( ph -> M e. ZZ ) $.
    climcn1.3 $e |- ( ph -> A e. B ) $.
    climcn1.4 $e |- ( ( ph /\ z e. B ) -> ( F ` z ) e. CC ) $.
    climcn1.5 $e |- ( ph -> G ~~> A ) $.
    climcn1.6 $e |- ( ph -> H e. W ) $.
    climcn1.7 $e |- ( ( ph /\ x e. RR+ ) -> E. y e. RR+ A. z e. B
                    ( ( abs ` ( z - A ) ) < y ->
                      ( abs ` ( ( F ` z ) - ( F ` A ) ) ) < x ) ) $.
    climcn1.8 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) e. B ) $.
    climcn1.9 $e |- ( ( ph /\ k e. Z ) -> ( H ` k ) = ( F ` ( G ` k ) ) ) $.
    $( Image of a limit under a continuous map.  (Contributed by Mario
       Carneiro, 31-Jan-2014.) $)
    climcn1 $p |- ( ph -> H ~~> ( F ` A ) ) $=
      ( vj cfv cli wbr cv cmin co cabs clt cuz wral wrex crp wa wi adantr simpr
      wcel cz eqidd climi2 uztrn2 adantlr wceq oveq1 fveq2d breq1d fveq2 oveq1d
      imbi12d rcla4va sylan an32s sylan2 anassrs ralimdva reximdva ex rexlimdva
      mpid mpd ralrimiva cc eleq1d rcla4v sylc clim2c mpbird ) AJEHUDZUEUFGUGZI
      UDZHUDZWKUHUIZUJUDZBUGZUKUFZGUCUGZULUDZUMZUCMUNZBUOUMAXBBUOAWQUOUTZUPDUGZ
      EUHUIZUJUDZCUGZUKUFZXDHUDZWKUHUIZUJUDZWQUKUFZUQZDFUMZCUOUNZXBTAXOXBUQXCAX
      NXBCUOAXGUOUTZUPZXNWMEUHUIZUJUDZXGUKUFZGWTUMZUCMUNZXBXQEWMXGUCGIKFMNAKVAU
      TXPOURAXPUSXQWLMUTZUPZWMVBAIEUEUFXPRURAEFUTZXPPURVCXQXNYBXBUQXQXNUPZYAXAU
      CMYFWSMUTZUPXTWRGWTYFYGWLWTUTZXTWRUQZYGYHUPYFYCYIKWLWSMNVDXQYCXNYIYDWMFUT
      ZXNYIAYCYJXPUAVEXMYIDWMFXDWMVFZXHXTXLWRYKXFXSXGUKYKXEXRUJXDWMEUHVGVHVIYKX
      KWPWQUKYKXJWOUJYKXIWNWKUHXDWMHVJZVKVHVIVLVMVNVOVPVQVRVSVTWBWAURWCWDABWKWN
      UCGJKLMNOSUBAYEXIWEUTZDFUMZWKWEUTZPAYMDFQWDZYMYODEFXDEVFXIWKWEXDEHVJWFWGW
      HAYCUPYJYNWNWEUTZUAAYNYCYPURYMYQDWMFYKXIWNWEYLWFWGWHWIWJ $.
      $( [31-Jan-2014] $)
  $}

  ${
    $d j k u v C $.  $d j k u v D $.  $d j k v y z H $.  $d j k u v x y z ph $.
    $d j k u v x y z A $.  $d j k u v y z G $.  $d j k x K $.  $d j k y z Z $.
    $d j k u v x y z B $.  $d j k u v x y z F $.  $d j M $.
    climcn2.1 $e |- Z = ( ZZ>= ` M ) $.
    climcn2.2 $e |- ( ph -> M e. ZZ ) $.
    climcn2.3a $e |- ( ph -> A e. C ) $.
    climcn2.3b $e |- ( ph -> B e. D ) $.
    climcn2.4 $e |- ( ( ph /\ ( u e. C /\ v e. D ) ) -> ( u F v ) e. CC ) $.
    climcn2.5a $e |- ( ph -> G ~~> A ) $.
    climcn2.5b $e |- ( ph -> H ~~> B ) $.
    climcn2.6 $e |- ( ph -> K e. W ) $.
    climcn2.7 $e |- ( ( ph /\ x e. RR+ ) ->
      E. y e. RR+ E. z e. RR+ A. u e. C A. v e. D
        ( ( ( abs ` ( u - A ) ) < y /\ ( abs ` ( v - B ) ) < z ) ->
          ( abs ` ( ( u F v ) - ( A F B ) ) ) < x ) ) $.
    climcn2.8a $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) e. C ) $.
    climcn2.8b $e |- ( ( ph /\ k e. Z ) -> ( H ` k ) e. D ) $.
    climcn2.9 $e |- ( ( ph /\ k e. Z ) ->
                      ( K ` k ) = ( ( G ` k ) F ( H ` k ) ) ) $.
    $( Image of a limit under a continuous map, two-arg version.  (Contributed
       by Mario Carneiro, 31-Jan-2014.) $)
    climcn2 $p |- ( ph -> K ~~> ( A F B ) ) $=
      ( vj co cli wbr cv cfv cmin cabs clt cuz wral wrex crp wcel adantr simprl
      wa wi cz eqidd climi2 simprr rexanuz2 sylanbrc uztrn2 oveq1 fveq2d breq1d
      wceq anbi1d oveq1d imbi12d anbi2d oveq2 rcla42v syl2anc imp an32s anassrs
      sylan2 ralimdva reximdva ex mpid rexlimdvv ralrimiva cc ralrimivva eleq1d
      mpd jca sylc clim2c mpbird ) AOGHLULZUMUNKUOZMUPZXFNUPZLULZXEUQULZURUPZBU
      OZUSUNZKUKUOZUTUPZVAZUKRVBZBVCVAAXQBVCAXLVCVDZVGFUOZGUQULZURUPZCUOZUSUNZE
      UOZHUQULZURUPZDUOZUSUNZVGZXSYDLULZXEUQULZURUPZXLUSUNZVHZEJVAFIVAZDVCVBCVC
      VBZXQUGAYPXQVHXRAYOXQCDVCVCAYBVCVDZYGVCVDZVGZYOXQVHAYSVGZYOXGGUQULZURUPZY
      BUSUNZXHHUQULZURUPZYGUSUNZVGZKXOVAZUKRVBZXQYTUUCKXOVAUKRVBUUFKXOVAUKRVBUU
      IYTGXGYBUKKMPIRSAPVIVDYSTVEZAYQYRVFYTXFRVDZVGZXGVJAMGUMUNYSUDVEAGIVDZYSUA
      VEVKYTHXHYGUKKNPJRSUUJAYQYRVLUULXHVJANHUMUNYSUEVEAHJVDZYSUBVEVKUUCUUFUKKP
      RSVMVNAYOUUIXQVHZVHYSAYOUUOAYOVGZUUHXPUKRUUPXNRVDZVGUUGXMKXOUUPUUQXFXOVDZ
      UUGXMVHZUUQUURVGUUPUUKUUSPXFXNRSVOAUUKYOUUSAUUKVGZYOUUSUUTXGIVDZXHJVDZYOU
      USVHUHUIYNUUSUUCYHVGZXGYDLULZXEUQULZURUPZXLUSUNZVHFEXGXHIJXSXGVSZYIUVCYMU
      VGUVHYCUUCYHUVHYAUUBYBUSUVHXTUUAURXSXGGUQVPVQVRVTUVHYLUVFXLUSUVHYKUVEURUV
      HYJUVDXEUQXSXGYDLVPZWAVQVRWBYDXHVSZUVCUUGUVGXMUVJYHUUFUUCUVJYFUUEYGUSUVJY
      EUUDURYDXHHUQVPVQVRWCUVJUVFXKXLUSUVJUVEXJURUVJUVDXIXEUQYDXHXGLWDZWAVQVRWB
      WEWFWGWHWJWIWKWLWMVEWNWMWOVEWTWPABXEXIUKKOPQRSTUFUJAYJWQVDZEJVAFIVAZXEWQV
      DZAUVLFEIJUCWRZAUUMUUNUVMUVNVHUAUBUVLUVNGYDLULZWQVDFEGHIJXSGVSYJUVPWQXSGY
      DLVPWSYDHVSUVPXEWQYDHGLWDWSWEWFWTUUTUVAUVBVGUVMXIWQVDZUUTUVAUVBUHUIXAAUVM
      UUKUVOVEUVLUVQUVDWQVDFEXGXHIJUVHYJUVDWQUVIWSUVJUVDXIWQUVKWSWEXBXCXD $.
      $( [31-Jan-2014] $)
  $}

  ${
    $d u v w y z A $.  $d u v w y z B $.  $d u v w y z C $.
    $( Complex number addition is a continuous function.  Part of Proposition
       14-4.16 of [Gleason] p. 243.  (We write out the definition directly
       because ~ df-cn and ~ df-cncf are not yet available to us.  See ~ addcn
       for the abbreviated version.)  (Contributed by Mario Carneiro,
       31-Jan-2014.) $)
    addcn2 $p |- ( ( A e. RR+ /\ B e. CC /\ C e. CC ) ->
      E. y e. RR+ E. z e. RR+ A. u e. CC A. v e. CC
        ( ( ( abs ` ( u - B ) ) < y /\ ( abs ` ( v - C ) ) < z ) ->
          ( abs ` ( ( u + v ) - ( B + C ) ) ) < A ) ) $=
      ( crp wcel cc co cv cmin cabs cfv clt wbr wa wi wral w3a c2 cdiv rphalfcl
      caddc wrex wceq simprl simpl2 simprr pnpcan2 syl3anc fveq2d breq1d simpl3
      3ad2ant1 pnpcan anbi12d ax-addcl adantl syl2anc rpre syl abs3lem syl22anc
      cr simpl1 sylbird ralrimivva breq2 anbi1d imbi1d 2ralbidv anbi2d rcla42ev
      ) EHIZFJIZGJIZUAZEUBUCKZHIZWADLZFMKZNOZVTPQZCLZGMKZNOZVTPQZRZWBWFUEKZFGUE
      KZMKNOEPQZSZCJTDJTZWDALZPQZWHBLZPQZRZWMSZCJTDJTZBHUFAHUFVPVQWAVREUDUPZXCV
      SWNDCJJVSWBJIZWFJIZRZRZWJWKFWFUEKZMKZNOZVTPQZXHWLMKZNOZVTPQZRZWMXGXKWEXNW
      IXGXJWDVTPXGXIWCNXGXDVQXEXIWCUGVSXDXEUHVPVQVRXFUIZVSXDXEUJZWBFWFUKULUMUNX
      GXMWHVTPXGXLWGNXGVQXEVRXLWGUGXPXQVPVQVRXFUOZFWFGUQULUMUNURXGWKJIZWLJIZXHJ
      IZEVFIZXOWMSXFXSVSWBWFUSUTXGVQVRXTXPXRFGUSVAXGVQXEYAXPXQFWFUSVAXGVPYBVPVQ
      VRXFVGEVBVCWKWLXHEVDVEVHVIXBWOWEWSRZWMSZCJTDJTABVTVTHHWPVTUGZXAYDDCJJYEWT
      YCWMYEWQWEWSWPVTWDPVJVKVLVMWRVTUGZYDWNDCJJYFYCWJWMYFWSWIWEWRVTWHPVJVNVLVM
      VOUL $.
      $( [31-Jan-2014] $)

    $( Complex number subtraction is a continuous function.  Part of
       Proposition 14-4.16 of [Gleason] p. 243.  (Contributed by Mario
       Carneiro, 31-Jan-2014.) $)
    subcn2 $p |- ( ( A e. RR+ /\ B e. CC /\ C e. CC ) ->
      E. y e. RR+ E. z e. RR+ A. u e. CC A. v e. CC
        ( ( ( abs ` ( u - B ) ) < y /\ ( abs ` ( v - C ) ) < z ) ->
          ( abs ` ( ( u - v ) - ( B - C ) ) ) < A ) ) $=
      ( vw crp wcel cc cv cmin co cabs cfv clt wbr wa wceq cneg caddc wral wrex
      w3a negcl addcn2 syl3an3 oveq1 fveq2d breq1d anbi2d oveq1d imbi12d rcla4v
      wi oveq2 adantl simpr simpll3 neg2sub syl2anc abssub eqtrd negsub adantll
      syl simpll2 oveq12d sylibd ralrimdva ralimdva reximdv mpd ) EIJZFKJZGKJZU
      EZDLZFMNOPALQRZHLZGUAZMNZOPZBLZQRZSZVSWAUBNZFWBUBNZMNZOPZEQRZUPZHKUCZDKUC
      ZBIUDZAIUDZVTCLZGMNOPZWEQRZSZVSWRMNZFGMNZMNZOPZEQRZUPZCKUCZDKUCZBIUDZAIUD
      VQVOVPWBKJWQGUFABHDEFWBUGUHVRWPXJAIVRWOXIBIVRWNXHDKVRVSKJZSZWNXGCKXLWRKJZ
      SZWNVTWRUAZWBMNZOPZWEQRZSZVSXOUBNZWIMNZOPZEQRZUPZXGXMWNYDUPZXLXMXOKJYEWRU
      FWMYDHXOKWAXOTZWGXSWLYCYFWFXRVTYFWDXQWEQYFWCXPOWAXOWBMUIUJUKULYFWKYBEQYFW
      JYAOYFWHXTWIMWAXOVSUBUQUMUJUKUNUOVGURXNXSXAYCXFXNXRWTVTXNXQWSWEQXNXQGWRMN
      ZOPZWSXNXPYGOXNXMVQXPYGTXLXMUSZVOVPVQXKXMUTZWRGVAVBUJXNVQXMYHWSTYJYIGWRVC
      VBVDUKULXNYBXEEQXNYAXDOXNXTXBWIXCMXKXMXTXBTVRVSWRVEVFXNVPVQWIXCTVOVPVQXKX
      MVHYJFGVEVBVIUJUKUNVJVKVLVMVMVN $.
      $( [31-Jan-2014] $)

    $( Complex number multiplication is a continuous function.  Part of
       Proposition 14-4.16 of [Gleason] p. 243.  (Contributed by Mario
       Carneiro, 31-Jan-2014.) $)
    mulcn2 $p |- ( ( A e. RR+ /\ B e. CC /\ C e. CC ) ->
      E. y e. RR+ E. z e. RR+ A. u e. CC A. v e. CC
        ( ( ( abs ` ( u - B ) ) < y /\ ( abs ` ( v - C ) ) < z ) ->
          ( abs ` ( ( u x. v ) - ( B x. C ) ) ) < A ) ) $=
      ( crp wcel cc co cabs cfv clt wbr wa wi cr cle syl2anc w3a c2 c1 caddc cv
      cdiv cmin cmul wral rphalfcl 3ad2ant1 cc0 3ad2ant3 3ad2ant2 1re ax-addrcl
      wrex sylancl absge0 lt01 addgegt0 an4s mpanr12 elrp sylanbrc rpdivcl rpre
      abscl syl sylan2b syl21anc wb simprl simpl2 subcl adantr ltmuldiv syl3anc
      syl3an3b simprr simpl3 abs2dif resubcl lelttr mpand ltsubadd2 sylibd ltle
      syld lemul2a ex syl112anc ax-mulrcl exp3a 3syld com23 sylbird wceq absmul
      imp3a subdir fveq2d eqtr3d sylib ltmuldiv2 subdi lep1 jca lemul1a syl3an3
      breq1d mpd eqbrtrd ax-mulcl adantld jcad adantl simpl1 abs3lem ralrimivva
      syl22anc breq2 anbi1d imbi1d 2ralbidv anbi2d rcla42ev ) EHIZFJIZGJIZUAZEU
      BUFKZGLMZYLFLMZUCUDKZUFKZUDKZUFKZHIZYPHIZDUEZFUGKZLMZYRNOZCUEZGUGKZLMZYPN
      OZPZUUAUUEUHKZFGUHKZUGKLMENOZQZCJUIDJUIZUUCAUEZNOZUUGBUEZNOZPZUULQZCJUIDJ
      UIZBHUQAHUQYKYLHIZYQHIZYSYHYIUVBYJEUJUKZYKYQRIZULYQNOZUVCYKYMRIZYPRIZUVEY
      JYHUVGYIGVHUMZYKYTUVHYKUVBYOHIZYTUVDYKYORIZULYONOZUVJYKYNRIZUCRIZUVKYIYHU
      VMYJFVHZUNUOYNUCUPURZYIYHUVLYJYIUVMULYNSOZUVLUVOFUSUVMUVQPUVNULUCNOZUVLUO
      UTUVMUVNUVQUVRUVLYNUCVAVBVCTUNYOVDZVEZYLYOVFTZYPVGVIZYMYPUPTZYKUVGULYMSOZ
      YTUVFUVIYJYHUWDYIGUSUMUWAYTUVGUWDPUVHULYPNOZPUVFYPVDUVGUVHUWDUWEUVFYMYPVA
      VBVJVKYQVDZVEZYLYQVFTUWAYKUUMDCJJYKUUAJIZUUEJIZPZPZUUIUUJFUUEUHKZUGKZLMZY
      LNOZUWLUUKUGKZLMZYLNOZPZUULUWKUUIUWOUWRUWKUUIUUCUUELMZUHKZYLNOZUWOUWKUUDU
      UHUXBUWKUUDUUCYQUHKZYLNOZUUHUXBQUWKUUCRIZYLRIZUVCUXDUUDVLZUWKUUBJIZUXEUWK
      UWHYIUXHYKUWHUWIVMZYHYIYJUWJVNZUUAFVOTZUUBVHVIZUWKUVBUXFYKUVBUWJUVDVPYLVG
      VIZYKUVCUWJUWGVPUVCUXEUXFUVEUVFPUXGUWFUUCYLYQVQVSVRUWKUUHUXDUXBUWKUUHUWTY
      QSOZUXAUXCSOZUXDUXBQUWKUUHUWTYQNOZUXNUWKUUHUWTYMUGKZYPNOZUXPUWKUXQUUGSOZU
      UHUXRUWKUWIYJUXSYKUWHUWIVTZYHYIYJUWJWAZUUEGWBTUWKUXQRIZUUGRIZUVHUXSUUHPUX
      RQUWKUWTRIZUVGUYBUWKUWIUYDUXTUUEVHVIZYKUVGUWJUVIVPZUWTYMWCTUWKUUFJIZUYCUW
      KUWIYJUYGUXTUYAUUEGVOTZUUFVHZVIZYKUVHUWJUWBVPZUXQUUGYPWDVRWEUWKUYDUVGUVHU
      XRUXPVLUYEUYFUYKUWTYMYPWFVRWGUWKUYDUVEUXPUXNQUYEYKUVEUWJUWCVPZUWTYQWHTWIU
      WKUYDUVEUXEULUUCSOZUXNUXOQUYEUYLUXLUWKUXHUYMUXKUUBUSVIUYDUVEUXEUYMPUAUXNU
      XOUWTYQUUCWJWKWLUWKUXOUXDUXBUWKUXARIZUXCRIZUXFUXOUXDPUXBQUWKUXEUYDUYNUXLU
      YEUUCUWTWMTUWKUXEUVEUYOUXLUYLUUCYQWMTUXMUXAUXCYLWDVRWNWOWPWQWTUWKUXAUWNYL
      NUWKUUBUUEUHKZLMZUXAUWNUWKUXHUWIUYQUXAWRUXKUXTUUBUUEWSTUWKUYPUWMLUWKUWHYI
      UWIUYPUWMWRUXIUXJUXTUUAFUUEXAVRXBXCXKWGUWKUUHUWRUUDUWKUUHYOUUGUHKZYLNOZUW
      RUWKUYCUXFUVKUVLPZUYSUUHVLUYJUXMUWKUVJUYTYKUVJUWJUVTVPUVSXDUUGYLYOXEVRUWK
      UWQUYRSOZUYSUWRUWKUWQYNUUGUHKZUYRSUWKFUUFUHKZLMZUWQVUBUWKVUCUWPLUWKYIUWIY
      JVUCUWPWRUXJUXTUYAFUUEGXFVRXBUWKYIUYGVUDVUBWRUXJUYHFUUFWSTXCUWKYNYOSOZVUB
      UYRSOZUWKUVMVUEUWKYIUVMUXJUVOVIZYNXGVIUWKUVMUVKUYGVUEVUFQZVUGYKUVKUWJUVPV
      PZUYHUYGUVMUVKUYCULUUGSOZPZVUHUYGUYCVUJUYIUUFUSXHUVMUVKVUKUAVUEVUFYNYOUUG
      XIWKXJVRXLXMUWKUWQRIZUYRRIZUXFVUAUYSPUWRQUWKUWPJIZVULUWKUWLJIZUUKJIZVUNUW
      KYIUWIVUOUXJUXTFUUEXNTZUWKYIYJVUPUXJUYAFGXNTZUWLUUKVOTUWPVHVIUWKUVKUYCVUM
      VUIUYJYOUUGWMTUXMUWQUYRYLWDVRWEWQXOXPUWKUUJJIZVUPVUOERIZUWSUULQUWJVUSYKUU
      AUUEXNXQVURVUQUWKYHVUTYHYIYJUWJXREVGVIUUJUUKUWLEXSYAWIXTUVAUUNUUDUURPZUUL
      QZCJUIDJUIABYRYPHHUUOYRWRZUUTVVBDCJJVVCUUSVVAUULVVCUUPUUDUURUUOYRUUCNYBYC
      YDYEUUQYPWRZVVBUUMDCJJVVDVVAUUIUULVVDUURUUHUUDUUQYPUUGNYBYFYDYEYGVR $.
      $( [31-Jan-2014] $)
  $}

  ${
    $d k u v x y z B $.  $d k C $.  $d j k u v y z F $.  $d j k u v x y z ph $.
    $d j k u v x y z A $.  $d j k v x y z G $.  $d k x y z H $.  $d j k x M $.
    $d j k x y z Z $.
    climadd.1 $e |- Z = ( ZZ>= ` M ) $.
    climadd.2 $e |- ( ph -> M e. ZZ ) $.
    climadd.3 $e |- ( ph -> A e. V ) $.
    climadd.4 $e |- ( ph -> F ~~> A ) $.
    ${
      climadd.5 $e |- ( ph -> B e. W ) $.
      climadd.6 $e |- ( ph -> H e. X ) $.
      climadd.7 $e |- ( ph -> G ~~> B ) $.
      climadd.8 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
      climadd.9 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) e. CC ) $.
      ${
        climadd.h $e |- ( ( ph /\ k e. Z ) ->
                          ( H ` k ) = ( ( F ` k ) + ( G ` k ) ) ) $.
        $( Limit of the sum of two converging sequences.  Proposition 12-2.1(a)
           of [Gleason] p. 168.  (The proof was shortened by Mario Carneiro,
           31-Jan-2014.) $)
        climadd $p |- ( ph -> H ~~> ( A + B ) ) $=
          ( vx vy vz vv vu cc caddc wcel cli wbr climcl syl2anc cv wa co adantl
          addcl crp cmin cabs cfv clt wi wral wrex simpr adantr syl3anc climcn2
          addcn2 ) AUCUDUEUFUGBCUHUHDUIEFGHKLMNABIUJEBUKULBUHUJZOPBEIUMUNZACJUJ
          FCUKULCUHUJZQSCFJUMUNZUGUOZUHUJUFUOZUHUJUPVQVRUIUQZUHUJAVQVRUSURPSRAU
          CUOZUTUJZUPWAVMVOVQBVAUQVBVCUDUOVDULVRCVAUQVBVCUEUOVDULUPVSBCUIUQVAUQ
          VBVCVTVDULVEUFUHVFUGUHVFUEUTVGUDUTVGAWAVHAVMWAVNVIAVOWAVPVIUDUEUFUGVT
          BCVLVJTUAUBVK $.
          $( [31-Jan-2014] $) $( [24-Sep-2005] $)
      $}

      ${
        climmul.h $e |- ( ( ph /\ k e. Z ) ->
                          ( H ` k ) = ( ( F ` k ) x. ( G ` k ) ) ) $.
        $( Limit of the product of two converging sequences.  Proposition
           12-2.1(c) of [Gleason] p. 168.  (The proof was shortened by Mario
           Carneiro, 1-Feb-2014.) $)
        climmul $p |- ( ph -> H ~~> ( A x. B ) ) $=
          ( vx vy vz vv vu cc cmul cli wbr climcl syl2anc cv wa co mulcl adantl
          wcel crp cmin cabs cfv clt wi wral wrex adantr mulcn2 syl3anc climcn2
          simpr ) AUCUDUEUFUGBCUHUHDUIEFGHKLMNABIUSEBUJUKBUHUSZOPBEIULUMZACJUSF
          CUJUKCUHUSZQSCFJULUMZUGUNZUHUSUFUNZUHUSUOVQVRUIUPZUHUSAVQVRUQURPSRAUC
          UNZUTUSZUOWAVMVOVQBVAUPVBVCUDUNVDUKVRCVAUPVBVCUEUNVDUKUOVSBCUIUPVAUPV
          BVCVTVDUKVEUFUHVFUGUHVFUEUTVGUDUTVGAWAVLAVMWAVNVHAVOWAVPVHUDUEUFUGVTB
          CVIVJTUAUBVK $.
          $( [1-Feb-2014] $) $( [27-Dec-2005] $)
      $}

      ${
        climsub.h $e |- ( ( ph /\ k e. Z ) ->
                          ( H ` k ) = ( ( F ` k ) - ( G ` k ) ) ) $.
        $( Limit of the difference of two converging sequences.  Proposition
           12-2.1(b) of [Gleason] p. 168.  (The proof was shortened by Mario
           Carneiro, 1-Feb-2014.) $)
        climsub $p |- ( ph -> H ~~> ( A - B ) ) $=
          ( vx vy vz vv vu cc cmin cli wbr climcl syl2anc cv wa co subcl adantl
          wcel crp cabs cfv clt wral wrex simpr adantr subcn2 syl3anc climcn2
          wi ) AUCUDUEUFUGBCUHUHDUIEFGHKLMNABIUSEBUJUKBUHUSZOPBEIULUMZACJUSFCUJ
          UKCUHUSZQSCFJULUMZUGUNZUHUSUFUNZUHUSUOVPVQUIUPZUHUSAVPVQUQURPSRAUCUNZ
          UTUSZUOVTVLVNVPBUIUPVAVBUDUNVCUKVQCUIUPVAVBUEUNVCUKUOVRBCUIUPUIUPVAVB
          VSVCUKVKUFUHVDUGUHVDUEUTVEUDUTVEAVTVFAVLVTVMVGAVNVTVOVGUDUEUFUGVSBCVH
          VITUAUBVJ $.
          $( [1-Feb-2014] $) $( [4-Aug-2007] $)
      $}
    $}

    ${
      climaddc1.5 $e |- ( ph -> C e. CC ) $.
      climaddc1.6 $e |- ( ph -> G e. W ) $.
      climaddc1.7 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
      ${
        climaddc1.h $e |- ( ( ph /\ k e. Z ) ->
                            ( G ` k ) = ( ( F ` k ) + C ) ) $.
        $( Limit of a constant ` C ` added to each term of a sequence.
           (Revised by Mario Carneiro, 3-Feb-2014.) $)
        climaddc1 $p |- ( ph -> G ~~> ( A + C ) ) $=
          ( cz cc csn cxp cc0 cli wbr 0z uzssz zex climconst2 sylancl cv wa cfv
          wcel wceq cuz eluzelz eleq2s fvconst2g syl2an adantr eqeltrd caddc co
          oveq2d eqtr4d climadd ) ABCDESCUAUBZFGHTIJKLMNOPACTUNZUCSUNVHCUDUEOUF
          CUCSUCUGUHUIUJQADUKZJUNZULZVJVHUMZCTAVIVJSUNZVMCUOVKOVNVJGUPUMJGVJUQK
          URSCVJTUSUTZAVIVKOVAVBVLVJFUMVJEUMZCVCVDVPVMVCVDRVLVMCVPVCVOVEVFVG $.
          $( [3-Feb-2014] $) $( [24-Sep-2005] $)
      $}

      ${
        climaddc2.h $e |- ( ( ph /\ k e. Z ) ->
                            ( G ` k ) = ( C + ( F ` k ) ) ) $.
        $( Limit of a constant ` C ` added to each term of a sequence.
           (Revised by Mario Carneiro, 3-Feb-2014.) $)
        climaddc2 $p |- ( ph -> G ~~> ( C + A ) ) $=
          ( caddc wcel co cli cv wa cfv cc wceq adantr addcom syl2anc climaddc1
          eqtrd wbr climcl breqtrd ) AFBCSUAZCBSUAZUBABCDEFGHIJKLMNOPQADUCZJTZU
          DZURFUECUREUEZSUAZVACSUAZRUTCUFTZVAUFTVBVCUGAVDUSOUHQCVAUIUJULUKABUFT
          ZVDUPUQUGABHTEBUBUMVEMNBEHUNUJOBCUIUJUO $.
          $( [3-Feb-2014] $) $( [24-Sep-2005] $)
      $}

      ${
        climmulc2.h $e |- ( ( ph /\ k e. Z ) ->
                            ( G ` k ) = ( C x. ( F ` k ) ) ) $.
        $( Limit of a sequence multiplied by a constant ` C ` .  Corollary
           12-2.2 of [Gleason] p. 171. $)
        climmulc2 $p |- ( ph -> G ~~> ( C x. A ) ) $=
          ( cz cc csn cxp cc0 cli wbr 0z uzssz zex climconst2 sylancl cv wa cfv
          wcel cuz eluzelz eleq2s fvconst2g syl2an adantr eqeltrd oveq1d eqtr4d
          wceq cmul co climmul ) ACBDSCUAUBZEFGTHIJKLOACTUNZUCSUNVHCUDUEOUFCUCS
          UCUGUHUIUJMPNADUKZJUNZULZVJVHUMZCTAVIVJSUNZVMCVDVKOVNVJGUOUMJGVJUPKUQ
          SCVJTURUSZAVIVKOUTVAQVLVJFUMCVJEUMZVEVFVMVPVEVFRVLVMCVPVEVOVBVCVG $.
          $( [3-Feb-2014] $) $( [24-Sep-2005] $)
      $}

      ${
        climsubc1.h $e |- ( ( ph /\ k e. Z ) ->
                            ( G ` k ) = ( ( F ` k ) - C ) ) $.
        $( Limit of a constant ` C ` subtracted from each term of a sequence.
           (Contributed by Mario Carneiro, 9-Feb-2014.) $)
        climsubc1 $p |- ( ph -> G ~~> ( A - C ) ) $=
          ( cz cc csn cxp cc0 cli wbr 0z uzssz zex climconst2 sylancl cv wa cfv
          wcel cuz eluzelz eleq2s fvconst2g syl2an adantr eqeltrd oveq2d eqtr4d
          wceq cmin co climsub ) ABCDESCUAUBZFGHTIJKLMNOPACTUNZUCSUNVHCUDUEOUFC
          UCSUCUGUHUIUJQADUKZJUNZULZVJVHUMZCTAVIVJSUNZVMCVDVKOVNVJGUOUMJGVJUPKU
          QSCVJTURUSZAVIVKOUTVAVLVJFUMVJEUMZCVEVFVPVMVEVFRVLVMCVPVEVOVBVCVG $.
          $( [9-Feb-2014] $)
      $}

      ${
        climsubc2.h $e |- ( ( ph /\ k e. Z ) ->
                            ( G ` k ) = ( C - ( F ` k ) ) ) $.
        $( Limit of a constant ` C ` minus each term of a sequence.  (Revised
           by Mario Carneiro, 9-Feb-2014.) $)
        climsubc2 $p |- ( ph -> G ~~> ( C - A ) ) $=
          ( cz cc csn cxp cc0 cli wbr 0z uzssz zex climconst2 sylancl cv wa cfv
          wcel cuz eluzelz eleq2s fvconst2g syl2an adantr eqeltrd oveq1d eqtr4d
          wceq cmin co climsub ) ACBDSCUAUBZEFGTHIJKLOACTUNZUCSUNVHCUDUEOUFCUCS
          UCUGUHUIUJMPNADUKZJUNZULZVJVHUMZCTAVIVJSUNZVMCVDVKOVNVJGUOUMJGVJUPKUQ
          SCVJTURUSZAVIVKOUTVAQVLVJFUMCVJEUMZVEVFVMVPVEVFRVLVMCVPVEVOVBVCVG $.
          $( [3-Feb-2014] $) $( [24-Sep-2005] $)
      $}
    $}

    ${
      climle.5 $e |- ( ph -> G ~~> B ) $.
      climle.6 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. RR ) $.
      climle.7 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) e. RR ) $.
      climle.8 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) <_ ( G ` k ) ) $.
      $( Comparison of the limits of two sequences.  (Contributed by Paul
         Chapman, 10-Sep-2007.)  (Revised by Mario Carneiro, 1-Feb-2014.) $)
      climle $p |- ( ph -> A <_ B ) $=
        ( wcel cle wbr vj cvv wa cc0 cmin co cv cfv cmpt cz adantr cli cuz fvex
        simpr eqeltri mptex cr adantlr recnd wceq fveq2 oveq12d eqid ovex fvmpt
        a1i adantl climsub resubcl syl2anc eqeltrd wb breqtrrd climge0 climrecl
        subge0 mpbird mpbid wn leid syl brprc biimparc sylan pm2.61dan ) ACUBRZ
        BCSTZAWGUCZUDCBUEUFZSTZWHWIWJDUAIUAUGZFUHZWLEUHZUEUFZUIZGIJAGUJRWGKUKZW
        ICBDFEWPGUBHUBIJWQAWGUOZAFCULTWGNUKZABHRWGLUKWPUBRWIUAIWOIGUMUHUBJGUMUN
        UPUQVGAEBULTWGMUKWIDUGZIRZUCZWTFUHZAXAXCURRZWGPUSZUTXBWTEUHZAXAXFURRZWG
        OUSZUTXAWTWPUHZXCXFUEUFZVAWIUAWTWOXJIWPWLWTVAWMXCWNXFUEWLWTFVBWLWTEVBVC
        WPVDXCXFUEVEVFVHZVIXBXIXJURXKXBXDXGXJURRXEXHXCXFVJVKVLXBUDXJXISXBUDXJST
        ZXFXCSTZAXAXMWGQUSXBXDXGXLXMVMXEXHXCXFVQVKVRXKVNVOWICURRBURRZWKWHVMWICD
        FGUBIJWQWSXEWRVPAXNWGABDEGHIJKMOLVPZUKCBVQVKVSABBSTZWGVTZWHAXNXPXOBWAWB
        XQWHXPBCSWCWDWEWF $.
        $( [1-Feb-2014] $) $( [10-Sep-2007] $)
    $}

    ${
      climsqueeze.5 $e |- ( ph -> G e. W ) $.
      climsqueeze.6 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. RR ) $.
      climsqueeze.7 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) e. RR ) $.
      ${
        climsqueeze.8 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) <_ ( G ` k ) ) $.
        climsqueeze.9 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) <_ A ) $.
        $( Convergence of a sequence sandwiched between another converging
           sequence and its limit.  (Revised by Mario Carneiro, 3-Feb-2014.) $)
        climsqueeze $p |- ( ph -> G ~~> A ) $=
          ( wbr wcel vx vj cli cv cfv cmin co cabs clt cuz wral wrex crp adantr
          wa cz simpr eqidd climi2 wi uztrn2 cr wb climrecl lesub2 syl3anc wceq
          cle mpbid abssuble0 letrd 3brtr4d adantlr cc ad2antrr resubcl syl2anc
          recnd abscl syl rpre ad2antlr lelttr sylan2 anassrs ralimdva reximdva
          mpand mpd ralrimiva clim2c mpbird ) AEBUCSCUDZEUEZBUFUGZUHUEZUAUDZUIS
          ZCUBUDZUJUEZUKZUBIULZUAUMUKAXBUAUMAWQUMTZUOZWMDUEZBUFUGZUHUEZWQUISZCW
          TUKZUBIULXBXDBXEWQUBCDFGIJAFUPTXCKUNAXCUQXDWMITZUOZXEURADBUCSXCMUNABG
          TXCLUNUSXDXIXAUBIXDWSITZUOXHWRCWTXDXLWMWTTZXHWRUTZXLXMUOXDXJXNFWMWSIJ
          VAXKWPXGVHSZXHWRAXJXOXCAXJUOZBWNUFUGZBXEUFUGZWPXGVHXPXEWNVHSZXQXRVHSZ
          QXPXEVBTZWNVBTZBVBTZXSXTVCOPAYCXJABCDFGIJKMOLVDZUNZXEWNBVEVFVIXPYBYCW
          NBVHSWPXQVGPYERWNBVJVFXPYAYCXEBVHSXGXRVGOYEXPXEWNBOPYEQRVKXEBVJVFVLVM
          XKWPVBTZXGVBTZWQVBTZXOXHUOWRUTXKWOVNTYFXKWOXKYBYCWOVBTAXJYBXCPVMAYCXC
          XJYDVOZWNBVPVQVRWOVSVTXKXFVNTYGXKXFXKYAYCXFVBTAXJYAXCOVMYIXEBVPVQVRXF
          VSVTXCYHAXJWQWAWBWPXGWQWCVFWHWDWEWFWGWIWJAUABWNUBCEFHIJKNXPWNURABYDVR
          XPWNPVRWKWL $.
          $( [3-Feb-2014] $) $( [6-Feb-2008] $)
      $}

      ${
        climsqueeze2.8 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) <_ ( F ` k ) ) $.
        climsqueeze2.9 $e |- ( ( ph /\ k e. Z ) -> A <_ ( G ` k ) ) $.
        $( Convergence of a sequence sandwiched between another converging
           sequence and its limit.  (Revised by Mario Carneiro, 3-Feb-2014.) $)
        climsqueeze2 $p |- ( ph -> G ~~> A ) $=
          ( wbr wcel vx vj cli cv cfv cmin co cabs clt cuz wral wrex crp adantr
          wa cz simpr eqidd climi2 wi uztrn2 cr wb climrecl lesub1 syl3anc wceq
          cle mpbid abssubge0 letrd 3brtr4d adantlr cc ad2antrr resubcl syl2anc
          recnd abscl syl rpre ad2antlr lelttr sylan2 anassrs ralimdva reximdva
          mpand mpd ralrimiva clim2c mpbird ) AEBUCSCUDZEUEZBUFUGZUHUEZUAUDZUIS
          ZCUBUDZUJUEZUKZUBIULZUAUMUKAXBUAUMAWQUMTZUOZWMDUEZBUFUGZUHUEZWQUISZCW
          TUKZUBIULXBXDBXEWQUBCDFGIJAFUPTXCKUNAXCUQXDWMITZUOZXEURADBUCSXCMUNABG
          TXCLUNUSXDXIXAUBIXDWSITZUOXHWRCWTXDXLWMWTTZXHWRUTZXLXMUOXDXJXNFWMWSIJ
          VAXKWPXGVHSZXHWRAXJXOXCAXJUOZWOXFWPXGVHXPWNXEVHSZWOXFVHSZQXPWNVBTZXEV
          BTZBVBTZXQXRVCPOAYAXJABCDFGIJKMOLVDZUNZWNXEBVEVFVIXPYAXSBWNVHSWPWOVGY
          CPRBWNVJVFXPYAXTBXEVHSXGXFVGYCOXPBWNXEYCPORQVKBXEVJVFVLVMXKWPVBTZXGVB
          TZWQVBTZXOXHUOWRUTXKWOVNTYDXKWOXKXSYAWOVBTAXJXSXCPVMAYAXCXJYBVOZWNBVP
          VQVRWOVSVTXKXFVNTYEXKXFXKXTYAXFVBTAXJXTXCOVMYGXEBVPVQVRXFVSVTXCYFAXJW
          QWAWBWPXGWQWCVFWHWDWEWFWGWIWJAUABWNUBCEFHIJKNXPWNURABYBVRXPWNPVRWKWL
          $.
          $( [3-Feb-2014] $) $( [14-Feb-2008] $)
      $}
    $}
  $}

  ${
    $d j k A $.  $d j k B $.  $d j k x y F $.  $d j k x y M $.  $d j k x y N $.
    $d j k x C $.  $d j k G $.  $d j k x y ph $.  $d j k x Z $.
    clim2ser.1 $e |- Z = ( ZZ>= ` M ) $.
    ${
      clim2ser.2 $e |- ( ph -> N e. Z ) $.
      clim2ser.3 $e |- ( ph -> A e. V ) $.
      clim2ser.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
      ${
        clim2ser.5 $e |- ( ph -> seq M ( + , F ) ~~> A ) $.
        $( The limit of an infinite series with an initial segment removed.
           (Contributed by Paul Chapman, 9-Feb-2007.)  (Revised by Mario
           Carneiro, 1-Feb-2014.) $)
        clim2ser $p |- ( ph -> seq ( N + 1 ) ( + , F ) ~~>
                               ( A - ( seq M ( + , F ) ` N ) ) ) $=
          ( caddc cfv co wcel syl cc cv vj vx vy cseq cvv cuz syl6eleq peano2uz
          c1 eqid cz eluzelz wf eluzel2 serf ffvelrn syl2anc seqex wa syl6eleqr
          a1i adantr uztrn2 sylan cmin addcl adantl w3a addass simpr cfz elfzuz
          wceq sylan2 adantlr seqsplit oveq1d syldan pncan2 eqtr2d climsubc1 )
          ABFNDEUDZOZUAWBNDFUINPZUDZWDGUEWDUFOZWFUJZAWDEUFOZQZWDUKQAFWHQZWIAFHW
          HJIUGZEFUHRZEWDULRZKMAHSWBUMZFHQWCSQZACDEHIAWJEUKQWKEFUNRLUOZJHSFWBUP
          UQZWEUEQANDWDURVAAUATZWFQZUSZWNWRHQZWRWBOZSQAWNWSWPVBAWDHQZWSXAAWDWHH
          WLIUTZEWRWDHIVCVDHSWRWBUPUQWTXBWCVEPWCWRWEOZNPZWCVEPZXEWTXBXFWCVEWTCU
          BUCNSDEFWRCTZSQZUBTZSQZUSXHXJNPZSQWTXHXJVFVGXIXKUCTZSQVHXLXMNPXHXJXMN
          PNPVMWTXHXJXMVIVGAWSVJAWJWSWKVBAXHEWRVKPQZXHDOSQZWSXNAXHHQZXOXNXHWHHX
          HEWRVLIUTLVNVOVPVQWTWOXESQZXGXEVMAWOWSWQVBAWFSWEUMWSXQACDWDWFWGWMAXHW
          FQZXPXOAXCXRXPXDEXHWDHIVCVDLVRUOWFSWRWEUPVDWCXEVSUQVTWA $.
          $( [1-Feb-2014] $) $( [9-Feb-2008] $)
      $}

      ${
        clim2ser2.5 $e |- ( ph -> seq ( N + 1 ) ( + , F ) ~~> A ) $.
        $( The limit of an infinite series with an initial segment added.
           (Contributed by Mario Carneiro, 27-Apr-2014.) $)
        clim2ser2 $p |- ( ph -> seq M ( + , F ) ~~>
                               ( A + ( seq M ( + , F ) ` N ) ) ) $=
          ( caddc cfv co wcel syl cc cv vj vx vy cseq cvv cuz syl6eleq peano2uz
          c1 eqid cz eluzelz wf eluzel2 serf ffvelrn seqex a1i syl6eleqr uztrn2
          syl2anc sylan syldan wa addcl adantl w3a wceq addass simpr adantr cfz
          elfzuz sylan2 adantlr seqsplit addcom eqtrd climaddc1 ) ABFNDEUDZOZUA
          NDFUINPZUDZVTWBGUEWBUFOZWDUJZAWBEUFOZQZWBUKQAFWFQZWGAFHWFJIUGZEFUHRZE
          WBULRZKMAHSVTUMFHQWASQZACDEHIAWHEUKQWIEFUNRLUOJHSFVTUPVAZVTUEQANDEUQU
          RAWDSWCUMUATZWDQZWNWCOZSQZACDWBWDWEWKACTZWDQZWRHQZWRDOSQZAWBHQWSWTAWB
          WFHWJIUSEWRWBHIUTVBLVCUOWDSWNWCUPVBZAWOVDZWNVTOWAWPNPZWPWANPZXCCUBUCN
          SDEFWNWRSQZUBTZSQZVDWRXGNPZSQXCWRXGVEVFXFXHUCTZSQVGXIXJNPWRXGXJNPNPVH
          XCWRXGXJVIVFAWOVJAWHWOWIVKAWREWNVLPQZXAWOXKAWTXAXKWRWFHWREWNVMIUSLVNV
          OVPXCWLWQXDXEVHAWLWOWMVKXBWAWPVQVAVRVS $.
          $( [1-Feb-2014] $) $( [9-Feb-2008] $)
      $}
    $}

    ${
      iserex.2 $e |- ( ph -> N e. Z ) $.
      iserex.3 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
      $( An infinite series converges, if and only if the series does with
         initial terms removed.  (Contributed by Paul Chapman, 9-Feb-2007.)
         (Revised by Mario Carneiro, 27-Apr-2014.) $)
      iserex $p |- ( ph -> ( seq M ( + , F ) e. dom ~~> <->
                             seq N ( + , F ) e. dom ~~> ) ) $=
        ( wceq c1 co cfv wcel caddc cseq cli syl wbr cvv cuz wo cdm wb syl6eleq
        cmin uzm1 wi seqeq1 eleq1d bicomd a1i wa simpll eluzelz zcn 3syl ax-1cn
        cc cz npcan sylancl seqeq1d simplr syl6eleqr fvex cv sylan simpr climdm
        sylib clim2ser eqbrtrrd breldm adantr eqbrtrd clim2ser2 impbida ex jaod
        seqex mpd ) AEDJZEKUFLZDUAMZNZUBZOCDPZQUCZNZOCEPZWINZUDZAEWENZWGAEFWEHG
        UEZDEUGRAWCWMWFWCWMUHAWCWLWJWCWKWHWIOCEDUIUJUKULAWFWMAWFUMZWJWLWPWJUMZW
        KWHQMZWDWHMZUFLZQSWLWQOCWDKOLZPZWKWTQWQAXBWKJZAWFWJUNZAXAEOCAEUSNZKUSNX
        AEJAWNEUTNXEWODEUOEUPUQUREKVAVBVCZRWQWRBCDWDTFGWQWDWEFAWFWJVDGVEWRTNWQW
        HQVFULWQABVGZFNZXGCMUSNZXDIVHWQWJWHWRQSWPWJVIWHVJVKVLVMWKWTQOCEWAVNRWPW
        LUMZWHWKQMZWSOLZQSWJXJXKBCDWDTFGWPWDFNWLWPWDWEFAWFVIGVEVOXKTNXJWKQVFULX
        JAXHXIAWFWLUNZIVHXJXBWKXKQXJAXCXMXFRXJWLWKXKQSWPWLVIWKVJVKVPVQWHXLQOCDW
        AVNRVRVSVTWB $.
        $( [27-Apr-2014] $) $( [9-Feb-2008] $)
    $}

    ${
      isermulc2.2 $e |- ( ph -> M e. ZZ ) $.
      isermulc2.3 $e |- ( ph -> A e. V ) $.
      isermulc2.4 $e |- ( ph -> C e. CC ) $.
      isermulc2.5 $e |- ( ph -> seq M ( + , F ) ~~> A ) $.
      isermulc2.6 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
      isermulc2.7 $e |- ( ( ph /\ k e. Z ) ->
                          ( G ` k ) = ( C x. ( F ` k ) ) ) $.
      $( Multiplication of an infinite series by a constant.  (Contributed by
         Paul Chapman, 14-Nov-2007.)  (Revised by Mario Carneiro,
         1-Feb-2014.) $)
      isermulc2 $p |- ( ph -> seq M ( + , G ) ~~> ( C x. A ) ) $=
        ( caddc wcel cc co vj vx cseq cvv seqex a1i wf cv serf ffvelrn sylan wa
        cfv cmul addcl adantl wceq adantr adddi 3expb cuz simpr syl6eleq elfzuz
        cfz syl6eleqr sylan2 adantlr seqdistr climmulc2 ) ABCUAQEGUCZQFGUCZGHUD
        IJKLNMVLUDRAQFGUEUFAISVKUGUAUHZIRZVMVKUMSRADEGIJKOUIISVMVKUJUKAVNULZDUB
        CQSUNFEGVMDUHZSRZUBUHZSRZULZVPVRQTZSRVOVPVRUOUPVOCSRZVTCWAUNTCVPUNTCVRU
        NTQTUQZAWBVNMURWBVQVSWCCVPVRUSUTUKVOVMIGVAUMZAVNVBJVCAVPGVMVETRZVPEUMZS
        RZVNWEAVPIRZWGWEVPWDIVPGVMVDJVFZOVGVHAWEVPFUMCWFUNTUQZVNWEAWHWJWIPVGVHV
        IVJ $.
        $( [1-Feb-2014] $) $( [14-Nov-2007] $)
    $}

    ${
      climlec2.2 $e |- ( ph -> M e. ZZ ) $.
      climlec2.3 $e |- ( ph -> A e. RR ) $.
      climlec2.4 $e |- ( ph -> F ~~> B ) $.
      climlec2.5 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. RR ) $.
      climlec2.6 $e |- ( ( ph /\ k e. Z ) -> A <_ ( F ` k ) ) $.
      $( Comparison of a constant to the limit of a sequence.  (Revised by
         Mario Carneiro, 1-Feb-2014.) $)
      climlec2 $p |- ( ph -> A <_ B ) $=
        ( cz csn cxp cr wcel cc0 cfv cc cli wbr recnd 0z zex climconst2 sylancl
        uzssz cv wa wceq cuz eluzelz eleq2s fvconst2g syl2an adantr eqeltrd cle
        eqbrtrd climle ) ABCDNBOPZEFQGHIJABUARSNRVCBUBUCABJUDUEBSNSUIUFUGUHKADU
        JZGRZUKZVDVCTZBQABQRZVDNRZVGBULVEJVIVDFUMTGFVDUNHUONBVDQUPUQZAVHVEJURUS
        LVFVGBVDETUTVJMVAVB $.
        $( [1-Feb-2014] $) $( [28-Feb-2008] $)
    $}

    ${
      iserle.2 $e |- ( ph -> M e. ZZ ) $.
      iserle.3 $e |- ( ph -> A e. V ) $.
      iserle.4 $e |- ( ph -> seq M ( + , F ) ~~> A ) $.
      iserle.5 $e |- ( ph -> seq M ( + , G ) ~~> B ) $.
      iserle.6 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. RR ) $.
      iserle.7 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) e. RR ) $.
      iserle.8 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) <_ ( G ` k ) ) $.
      $( Comparison of the limits of two infinite series.  (Contributed by Paul
         Chapman, 12-Nov-2007.)  (Revised by Mario Carneiro, 3-Feb-2014.) $)
      iserle $p |- ( ph -> A <_ B ) $=
        ( cr wcel cfv vj caddc wf cv serfre ffvelrn sylan wa cuz simpr syl6eleq
        cseq cfz co simpll elfzuz syl6eleqr adantl syl2anc cle wbr serle climle
        ) ABCUAUBEGULZUBFGULZGHIJKLMNAIRVDUCUAUDZISZVFVDTRSADEGIJKOUEIRVFVDUFUG
        AIRVEUCVGVFVETRSADFGIJKPUEIRVFVEUFUGAVGUHZDEFGVFVHVFIGUITZAVGUJJUKVHDUD
        ZGVFUMUNSZUHZAVJISZVJETZRSAVGVKUOZVKVMVHVKVJVIIVJGVFUPJUQURZOUSVLAVMVJF
        TZRSVOVPPUSVLAVMVNVQUTVAVOVPQUSVBVC $.
        $( [3-Feb-2014] $) $( [12-Nov-2007] $)
    $}

    ${
      iserge0.2 $e |- ( ph -> M e. ZZ ) $.
      iserge0.3 $e |- ( ph -> seq M ( + , F ) ~~> A ) $.
      iserge0.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. RR ) $.
      iserge0.5 $e |- ( ( ph /\ k e. Z ) -> 0 <_ ( F ` k ) ) $.
      $( The limit of an infinite series of nonnegative reals is nonnegative.
         (Contributed by Paul Chapman, 9-Feb-2007.) $)
      iserge0 $p |- ( ph -> 0 <_ A ) $=
        ( cc0 cuz cfv csn cxp cr wcel 0re syl a1i cz caddc cseq cli serclim0 cv
        wbr wa wceq simpr syl6eleq elexi fvconst2 syl6eqel cle eqbrtrd iserle )
        ALBCEMNZLOPZDEQFGHLQRASUAAEUBRUCUTEUDLUEUHHEUFTIACUGZFRZUIZVAUTNZLQVCVA
        USRVDLUJVCVAFUSAVBUKGULUSLVALQSUMUNTZSUOJVCVDLVADNUPVEKUQUR $.
        $( [3-Feb-2014] $) $( [9-Feb-2008] $)
    $}

    ${
      climub.2 $e |- ( ph -> N e. Z ) $.
      climub.3 $e |- ( ph -> F ~~> A ) $.
      climub.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. RR ) $.
      climub.5 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) <_ ( F ` ( k + 1 ) ) ) $.
      $( The limit of a monotonic sequence is an upper bound.  (Revised by
         Mario Carneiro, 10-Feb-2014.) $)
      climub $p |- ( ph -> ( F ` N ) <_ A ) $=
        ( vj cfv cuz wcel cr wi syldan co eqid cz syl6eleq eluzelz syl cv fveq2
        eleq1d imbi2d expcom vtoclga mpcom uztrn2 sylan impcom simpr cfz elfzuz
        wceq wa sylan2 adantlr c1 cmin caddc cle wbr monoord climlec2 ) AFDNZBM
        DFFONZVKUAAFEONZPFUBPAFGVLIHUCEFUDUEFGPZAVJQPZIACUFZDNZQPZRZAVNRCFGVOFU
        SZVQVNAVSVPVJQVOFDUGUHUIAVOGPZVQKUJZUKULJAMUFZVKPZWBGPZWBDNZQPZAVMWCWDI
        EWBFGHUMUNWDAWFVRAWFRCWBGVOWBUSZVQWFAWGVPWEQVOWBDUGUHUIWAUKUOSAWCUTCDFW
        BAWCUPAVOFWBUQTPZVQWCWHAVOVKPZVQVOFWBURAWIVTVQAVMWIVTIEVOFGHUMUNZKSVAVB
        AVOFWBVCVDTZUQTPZVPVOVCVETDNVFVGZWCWLAWIWMVOFWKURAWIVTWMWJLSVAVBVHVI $.
        $( [10-Feb-2014] $) $( [18-Mar-2005] $)
    $}

    ${
      climserle.2 $e |- ( ph -> N e. Z ) $.
      climserle.3 $e |- ( ph -> seq M ( + , F ) ~~> A ) $.
      climserle.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. RR ) $.
      climserle.5 $e |- ( ( ph /\ k e. Z ) -> 0 <_ ( F ` k ) ) $.
      $( The partial sums of a converging infinite series with nonnegative
         terms are bounded by its limit.  (Revised by Mario Carneiro,
         9-Feb-2014.) $)
      climserle $p |- ( ph -> ( seq M ( + , F ) ` N ) <_ A ) $=
        ( caddc cr wcel cfv cle cc0 wbr wi vj wf cv cuz cz syl6eleq eluzel2 syl
        cseq serfre ffvelrn sylan wa c1 co peano2uzs fveq2 breq2d imbi2d expcom
        wceq vtoclga impcom sylan2 wb eleq1d addge01 mpbid simpr seqp1 breqtrrd
        syl2anc climub ) ABUAMDEUIZEFGHIJAGNVNUBUAUCZGOZVOVNPZNOZACDEGHAFEUDPZO
        EUEOAFGVSIHUFEFUGUHKUJGNVOVNUKULZAVPUMZVQVQVOUNMUOZDPZMUOZWBVNPZQWARWCQ
        SZVQWDQSZVPAWBGOZWFEVOGHUPZWHAWFARCUCZDPZQSZTAWFTCWBGWJWBVAZWLWFAWMWKWC
        RQWJWBDUQZURUSAWJGOZWLLUTVBVCVDWAVRWCNOZWFWGVEVTVPAWHWPWIWHAWPAWKNOZTAW
        PTCWBGWMWQWPAWMWKWCNWNVFUSAWOWQKUTVBVCVDVQWCVGVLVHWAVOVSOWEWDVAWAVOGVSA
        VPVIHUFMDEVOVJUHVKVM $.
        $( [9-Feb-2014] $) $( [27-Dec-2005] $)
    $}
  $}

  ${
    isershft.1 $e |- F e. _V $.
    $( Index shift of the limit of an infinite series. $)
    isershft $p |- ( ( M e. ZZ /\ N e. ZZ /\ A e. V ) ->
        ( seq M ( P , F ) ~~> A <->
          seq ( M + N ) ( P , ( F shift N ) ) ~~> A ) ) $=
      ( cz wcel w3a cshi co caddc cseq cli wbr wceq cc zcn cvv wa cmin sylancom
      zaddcl seqshft pncan syl2an seqeq1d oveq1d eqtrd breq1d wb seqex climshft
      3adant3 mp3an2 3adant1 bitr2d ) DHIZEHIZAFIZJZBCEKLDEMLZNZAOPBCDNZEKLZAOP
      ZVEAOPZVBVDVFAOUSUTVDVFQVAUSUTUAZVDBCVCEUBLZNZEKLZVFUSUTVCHIVDVLQDEUDBCVC
      EGUEUCVIVKVEEKVIVJDBCUSDRIERIVJDQUTDSESDEUFUGUHUIUJUOUKUTVAVGVHULZUSUTVET
      IVAVMBCDUMAVEETFUNUPUQUR $.
      $( [5-Nov-2013] $) $( [6-Sep-2013] $)
  $}

  ${
    $d x y z $.  $d y z A $.  $d y F $.
    cn1lem.1 $e |- F : CC --> CC $.
    cn1lem.2 $e |- ( ( z e. CC /\ A e. CC ) ->
                ( abs ` ( ( F ` z ) - ( F ` A ) ) ) <_ ( abs ` ( z - A ) ) ) $.
    $( A sufficient condition for a function to be continuous.  (Contributed by
       Mario Carneiro, 9-Feb-2014.) $)
    cn1lem $p |- ( ( A e. CC /\ x e. RR+ ) -> E. y e. RR+ A. z e. CC
                  ( ( abs ` ( z - A ) ) < y ->
                    ( abs ` ( ( F ` z ) - ( F ` A ) ) ) < x ) ) $=
      ( cc wcel cv crp wa cmin cfv clt wbr wi syl2anc cr syl co cabs wral simpr
      wrex cle simpll ffvelrni subcl abscl rpre ad2antlr lelttr mpand ralrimiva
      syl3anc wceq breq2 imbi1d ralbidv rcla4ev ) DHIZAJZKIZLZVDCJZDMUAZUBNZVCO
      PZVFENZDENZMUAZUBNZVCOPZQZCHUCZVHBJZOPZVNQZCHUCZBKUEVBVDUDVEVOCHVEVFHIZLZ
      VMVHUFPZVIVNWBWAVBWCVEWAUDZVBVDWAUGZGRWBVMSIZVHSIZVCSIZWCVILVNQWBVLHIZWFW
      BVJHIZVKHIZWIWBWAWJWDHHVFEFUHTWBVBWKWEHHDEFUHTVJVKUIRVLUJTWBVGHIZWGWBWAVB
      WLWDWEVFDUIRVGUJTVDWHVBWAVCUKULVMVHVCUMUPUNUOVTVPBVCKVQVCUQZVSVOCHWMVRVIV
      NVQVCVHOURUSUTVAR $.
      $( [9-Feb-2014] $)
  $}

  ${
    $d x y z $.  $d y z A $.
    $( The absolute value function is continuous.  (Contributed by Mario
       Carneiro, 9-Feb-2014.) $)
    abscn2 $p |- ( ( A e. CC /\ x e. RR+ ) -> E. y e. RR+ A. z e. CC
                    ( ( abs ` ( z - A ) ) < y ->
                      ( abs ` ( ( abs ` z ) - ( abs ` A ) ) ) < x ) ) $=
      ( cabs cc cr wf wss absf ax-resscn fss mp2an cv abs2difabs cn1lem ) ABCDE
      FGEHGFIFFEHJKFGFELMCNDOP $.
      $( [9-Feb-2014] $) $( [7-Sep-2007] $)

    $( The complex conjugate function is continuous.  (Contributed by Mario
       Carneiro, 9-Feb-2014.) $)
    cjcn2 $p |- ( ( A e. CC /\ x e. RR+ ) -> E. y e. RR+ A. z e. CC
                    ( ( abs ` ( z - A ) ) < y ->
                      ( abs ` ( ( * ` z ) - ( * ` A ) ) ) < x ) ) $=
      ( ccj cjf cv cc wcel wa cfv cmin co cabs cr wceq cle cjcl subcl syl abscl
      wbr syl2an cjsub fveq2d abscj eqtr3d eqle syl2anc cn1lem ) ABCDEFCGZHIZDH
      IZJZUKEKZDEKZLMZNKZOIZURUKDLMZNKZPURVAQUBUNUQHIZUSULUOHIUPHIVBUMUKRDRUOUP
      SUCUQUATUNUTEKZNKZURVAUNVCUQNUKDUDUEUNUTHIVDVAPUKDSUTUFTUGURVAUHUIUJ $.
      $( [9-Feb-2014] $) $( [7-Jun-2006] $)

    $( The real part function is continuous.  (Contributed by Mario Carneiro,
       9-Feb-2014.) $)
    recn2 $p |- ( ( A e. CC /\ x e. RR+ ) -> E. y e. RR+ A. z e. CC
                    ( ( abs ` ( z - A ) ) < y ->
                      ( abs ` ( ( Re ` z ) - ( Re ` A ) ) ) < x ) ) $=
      ( cre cc cr wf wss ref ax-resscn fss mp2an cv wcel cmin co cfv cabs cle
      wa resub fveq2d wbr subcl absrele syl eqbrtrrd cn1lem ) ABCDEFGEHGFIFFEHJ
      KFGFELMCNZFODFOUAZUJDPQZERZSRZUJERDERPQZSRULSRZTUKUMUOSUJDUBUCUKULFOUNUPT
      UDUJDUEULUFUGUHUI $.
      $( [9-Feb-2014] $) $( [24-Sep-2007] $)

    $( The imaginary part function is continuous.  (Contributed by Mario
       Carneiro, 9-Feb-2014.) $)
    imcn2 $p |- ( ( A e. CC /\ x e. RR+ ) -> E. y e. RR+ A. z e. CC
                    ( ( abs ` ( z - A ) ) < y ->
                      ( abs ` ( ( Im ` z ) - ( Im ` A ) ) ) < x ) ) $=
      ( cim cc cr wf wss imf ax-resscn fss mp2an cv wcel cmin co cfv cabs cle
      wa imsub fveq2d wbr subcl absimle syl eqbrtrrd cn1lem ) ABCDEFGEHGFIFFEHJ
      KFGFELMCNZFODFOUAZUJDPQZERZSRZUJERDERPQZSRULSRZTUKUMUOSUJDUBUCUKULFOUNUPT
      UDUJDUEULUFUGUHUI $.
      $( [9-Feb-2014] $) $( [24-Sep-2007] $)
  $}

  ${
    $d k x y z A $.  $d k y z F $.  $d k x G $.  $d k x y z ph $.  $d k y Z $.
    $d k x y z H $.
    climcn1lem.1 $e |- Z = ( ZZ>= ` M ) $.
    climcn1lem.2 $e |- ( ph -> F ~~> A ) $.
    climcn1lem.3 $e |- ( ph -> A e. V ) $.
    climcn1lem.4 $e |- ( ph -> G e. W ) $.
    climcn1lem.5 $e |- ( ph -> M e. ZZ ) $.
    climcn1lem.6 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
    ${
      climcn1lem.7 $e |- H : CC --> CC $.
      climcn1lem.8 $e |- ( ( A e. CC /\ x e. RR+ ) -> E. y e. RR+ A. z e. CC
                    ( ( abs ` ( z - A ) ) < y ->
                      ( abs ` ( ( H ` z ) - ( H ` A ) ) ) < x ) ) $.
      climcn1lem.9 $e |- ( ( ph /\ k e. Z ) ->
                           ( G ` k ) = ( H ` ( F ` k ) ) ) $.
      $( The limit of a continuous function, theorem form.  (Contributed by
         Mario Carneiro, 9-Feb-2014.) $)
      climcn1lem $p |- ( ph -> G ~~> ( H ` A ) ) $=
        ( cc wcel cli wbr climcl syl2anc cv cfv ffvelrni adantl crp cmin co clt
        cabs wi wral wrex sylan climcn1 ) ABCDEUCFIGHJLMNRAEKUDGEUEUFEUCUDZPOEG
        KUGUHZDUIZUCUDVEIUJZUCUDAUCUCVEITUKULOQAVCBUIZUMUDVEEUNUOUQUJCUIUPUFVFE
        IUJUNUOUQUJVGUPUFURDUCUSCUMUTVDUAVASUBVB $.
        $( [9-Feb-2014] $)
    $}

    ${
      climabs.7 $e |- ( ( ph /\ k e. Z ) ->
                        ( G ` k ) = ( abs ` ( F ` k ) ) ) $.
      $( Limit of the complex conjugate of a sequence.  Proposition 12-2.4(c)
         of [Gleason] p. 172.  (Revised by Mario Carneiro, 9-Feb-2014.) $)
      climabs $p |- ( ph -> G ~~> ( abs ` A ) ) $=
        ( vx cabs cc cr vy vz wf wss absf ax-resscn fss mp2an abscn2 climcn1lem
        ) AQUAUBBCDERFGHIJKLMNOSTRUCTSUDSSRUCUEUFSTSRUGUHQUAUBBUIPUJ $.
        $( [9-Feb-2014] $) $( [7-Jun-2006] $)
    $}

    ${
      climcj.7 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) = ( * ` ( F ` k ) ) ) $.
      $( Limit of the complex conjugate of a sequence.  Proposition 12-2.4(c)
         of [Gleason] p. 172.  (Revised by Mario Carneiro, 9-Feb-2014.) $)
      climcj $p |- ( ph -> G ~~> ( * ` A ) ) $=
        ( vx vy vz ccj cjf cjcn2 climcn1lem ) AQRSBCDETFGHIJKLMNOUAQRSBUBPUC $.
        $( [9-Feb-2014] $) $( [7-Jun-2006] $)
    $}

    ${
      climre.7 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) = ( Re ` ( F ` k ) ) ) $.
      $( Limit of the complex conjugate of a sequence.  Proposition 12-2.4(c)
         of [Gleason] p. 172.  (Revised by Mario Carneiro, 9-Feb-2014.) $)
      climre $p |- ( ph -> G ~~> ( Re ` A ) ) $=
        ( vx cre cc cr vy vz wf wss ref ax-resscn fss mp2an recn2 climcn1lem )
        AQUAUBBCDERFGHIJKLMNOSTRUCTSUDSSRUCUEUFSTSRUGUHQUAUBBUIPUJ $.
        $( [9-Feb-2014] $) $( [7-Jun-2006] $)
    $}

    ${
      climim.7 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) = ( Im ` ( F ` k ) ) ) $.
      $( Limit of the complex conjugate of a sequence.  Proposition 12-2.4(c)
         of [Gleason] p. 172.  (Revised by Mario Carneiro, 9-Feb-2014.) $)
      climim $p |- ( ph -> G ~~> ( Im ` A ) ) $=
        ( vx cim cc cr vy vz wf wss imf ax-resscn fss mp2an imcn2 climcn1lem )
        AQUAUBBCDERFGHIJKLMNOSTRUCTSUDSSRUCUEUFSTSRUGUHQUAUBBUIPUJ $.
        $( [9-Feb-2014] $) $( [7-Jun-2006] $)
    $}
  $}

  ${
    $d j k n x y F $.  $d j M $.  $d j k n y ph $.  $d j k n x y Z $.
    climsup.1 $e |- Z = ( ZZ>= ` M ) $.
    climsup.2 $e |- ( ph -> M e. ZZ ) $.
    climsup.3 $e |- ( ph -> F : Z --> RR ) $.
    climsup.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) <_ ( F ` ( k + 1 ) ) ) $.
    climsup.5 $e |- ( ph -> E. x e. RR A. k e. Z ( F ` k ) <_ x ) $.
    $( A bounded monotonic sequence converges to the supremum of its range.
       Theorem 12-5.1 of [Gleason] p. 180.  (Revised by Mario Carneiro,
       10-Feb-2014.) $)
    climsup $p |- ( ph -> F ~~> sup ( ran F , RR , < ) ) $=
      ( vy cr clt wbr cfv co wcel cle syl vj vn crn csup cli cmin cabs cuz wral
      cv wrex crp wa wss c0 wne w3a frn wfn ffn uzid syl6eleqr fnfvelrn syl2anc
      wf cz ne0i wb breq1 rexbidv mpbird 3jca adantr suprcl rpre resubcl syl2an
      ralrn ltsubrp sylan suprlub syl12anc breq2 rexrn biimpa syldan simprr cfz
      wi fzssuz uzss syl6sseqr eleq2s ad2antrl syl5ss ralrimiva ad2antrr ssralv
      ffvelrn sylc r19.21bi caddc ssel2 wceq fveq2 oveq1 fveq2d breq12d monoord
      c1 rcla4cva ad2ant2r uztrn2 lesub2 syl3anc ad2antlr lelttr ltsub23 suprub
      mpbid abssuble0 breq1d 3imtr4d anassrs ralrimdva reximdva mpd cvv eqeltri
      mpand fvex fex sylancl eqidd recnd clim2c ) ADDUCZMNUDZUEOCUJZDPZYRUFQUGP
      ZLUJZNOZCUAUJZUHPZUIZUAFUKZLULUIAUUGLULAUUBULRZUMZYRUUBUFQZUUDDPZNOZUAFUK
      ZUUGAUUHUUJYSNOZCYQUKZUUMUUIYQMUNZYQUOUPZUUBBUJZSOZLYQUIZBMUKZUQZUUJMRZUU
      JYRNOZUUOAUVBUUHAUUPUUQUVAAFMDVEZUUPIFMDURTAEDPZYQRZUUQADFUSZEFRUVGAUVEUV
      HIFMDUTTZAEEUHPZFAEVFREUVJRHEVATGVBFEDVCVDYQUVFVGTAUVAYTUURSOZCFUIZBMUKZK
      AUVHUVAUVMVHUVIUVHUUTUVLBMUUSUVKLCFDUUBYTUURSVIVRVJTVKVLZVMAYRMRZUUBMRZUV
      CUUHAUVBUVOUVNBLYQVNTZUUBVOZYRUUBVPVQAUVOUUHUVDUVQYRUUBVSVTBLCYQUUJWAWBAU
      UOUUMAUVHUUOUUMVHUVIUUNUULCUAFDYSUUKUUJNWCWDTWEWFUUIUULUUFUAFUUIUUDFRZUMU
      ULUUCCUUEUUIUVSYSUUERZUULUUCWIUUIUVSUVTUMZUMZYRUUKUFQZUUBNOZYRYTUFQZUUBNO
      ZUULUUCUWBUWEUWCSOZUWDUWFUWBUUKYTSOZUWGUWBUBDUUDYSUUIUVSUVTWGUWBUBUJZDPZM
      RZUBUUDYSWHQZUWBUWLFUNUWKUBFUIZUWKUBUWLUIUWBUWLUUEFUUDYSWJUVSUUEFUNZUUIUV
      TUWNUUDUVJFUUDUVJRUUEUVJFEUUDWKGWLGWMWNZWOAUWMUUHUWAAUVEUWMIUVEUWKUBFFMUW
      IDWSWPTWQUWKUBUWLFWRWTXAUWBUWIUUDYSXJUFQZWHQZRZUWIFRZUWJUWIXJXBQZDPZSOZUW
      BUWQFUNUWRUWSUWBUWQUUEFUUDUWPWJUWOWOUWQFUWIXCVTUWBYTYSXJXBQZDPZSOZCFUIZUW
      SUXBAUXFUUHUWAAUXECFJWPWQUXEUXBCUWIFYSUWIXDZYTUWJUXDUXASYSUWIDXEUXGUXCUWT
      DYSUWIXJXBXFXGXHXKVTWFXIUWBUUKMRZYTMRZUVOUWHUWGVHAUVSUXHUUHUVTAUVEUVSUXHI
      FMUUDDWSVTXLZUUIUVEYSFRZUXIUWAAUVEUUHIVMEYSUUDFGXMZFMYSDWSZVQZAUVOUUHUWAU
      VQWQZUUKYTYRXNXOXTUWBUWEMRZUWCMRZUVPUWGUWDUMUWFWIUWBUVOUXIUXPUXOUXNYRYTVP
      VDUWBUVOUXHUXQUXOUXJYRUUKVPVDUUHUVPAUWAUVRXPZUWEUWCUUBXQXOYJUWBUVOUVPUXHU
      ULUWDVHUXOUXRUXJYRUUBUUKXRXOUWBUUAUWEUUBNUWBUXIUVOYTYRSOZUUAUWEXDUXNUXOUW
      BUVBYTYQRZUXSAUVBUUHUWAUVNWQUUIUVHUXKUXTUWAAUVHUUHUVIVMUXLFYSDVCVQBLYQYTX
      SVDYTYRYAXOYBYCYDYEYFYGWPALYRYTUACDEYHFGHAUVEFYHRDYHRIFUVJYHGEUHYKYIFMYHD
      YLYMAUXKUMZYTYNAYRUVQYOUYAYTAUVEUXKUXIIUXMVTYOYPVK $.
      $( [10-Feb-2014] $) $( [13-Mar-2005] $)
  $}

  ${
    $d j k w x F $.  $d j k w x M $.  $d j k w Z $.
    climcau.1 $e |- Z = ( ZZ>= ` M ) $.
    $( A converging sequence of complex numbers is a Cauchy sequence.  Theorem
       12-5.3 of [Gleason] p. 180 (necessity part).  (Revised by Mario
       Carneiro, 26-Apr-2014.) $)
    climcau $p |- ( ( M e. ZZ /\ F e. dom ~~> ) -> A. x e. RR+ E. j e. Z
              A. k e. ( ZZ>= ` j ) ( abs ` ( ( F ` k ) - ( F ` j ) ) ) < x ) $=
      ( vw wcel cli cv cfv cmin co cabs clt wbr crp wa cc cdm cuz wral wrex cop
      cz wex eldm2g ibi df-br c2 cdiv cvv simpll rphalfcl adantl simplr vex a1i
      eqidd climi wi eluzelz uzid eleq2s wceq fveq2 eleq1d oveq1d fveq2d breq1d
      syl anbi12d rcla4v cr rpre ad2antlr simpllr climcl sylancr simprl simplrl
      simplll simprr abssub syl2anc simplrr eqbrtrd abs3lem 3impia syl222anc ex
      ralimdv com23 mpdd reximdva mpd ralrimiva syl5bir exlimdv syl5 imp ) EUFI
      ZDJUAZIZCKZDLZBKZDLZMNOLAKZPQZCXHUBLZUCZBFUDZARUCZXEDHKZUEJIZHUGZXCXOXEXR
      HDJXDUHUIXCXQXOHXQDXPJQZXCXODXPJUJXCXSXOXCXSSZXNARXTXJRIZSZXGTIZXGXPMNZOL
      ZXJUKULNZPQZSZCXLUCZBFUDXNYBXPXGYFBCDEUMFGXCXSYAUNYAYFRIXTXJUOUPYBXFFISXG
      UTXCXSYAUQXPUMIZYBHURZUSVAYBYIXMBFYBXHFIZSZYIXITIZXIXPMNZOLZYFPQZSZXMYMXH
      XLIZYIYRVBYLYSYBYSXHEUBLZFXHYTIXHUFIYSEXHVCXHVDVLGVEUPYHYRCXHXLXFXHVFZYCY
      NYGYQUUAXGXITXFXHDVGZVHUUAYEYPYFPUUAYDYOOUUAXGXIXPMUUBVIVJVKVMVNVLYMXJVOI
      ZXPTIZYIYRXMVBVBYAUUCXTYLXJVPVQYMYJXSUUDYKXCXSYAYLVRXPDUMVSVTUUCUUDSZYRYI
      XMUUEYRYIXMVBUUEYRSZYHXKCXLUUFYHXKUUFYHSZYCYNUUDUUCYGXPXIMNOLZYFPQZXKUUFY
      CYGWAUUEYNYQYHWBZUUCUUDYRYHVRZUUCUUDYRYHWCUUFYCYGWDUUGUUHYPYFPUUGUUDYNUUH
      YPVFUUKUUJXPXIWEWFUUEYNYQYHWGWHYCYNSUUDUUCSYGUUISXKXGXIXPXJWIWJWKWLWMWLWN
      WFWOWPWQWRWLWSWTXAXB $.
      $( [26-Apr-2014] $) $( [16-Apr-2005] $)
  $}

  ${
    $d j k m w x y z F $.  $d j k m w z M $.  $d j k x y z N $.  $d k m z ph $.
    $d j k x y z R $.  $d k m w x z S $.  $d j k m w x y z Z $.
    caurcvg.1 $e |- Z = ( ZZ>= ` M ) $.
    caurcvg.2 $e |- S = { y e. RR |
                          E. j e. Z A. k e. ( ZZ>= ` j ) y < ( F ` k ) } $.
    ${
      caucvglem.3 $e |- ( ph -> N e. Z ) $.
      caucvglem.4 $e |- ( ph -> R e. RR+ ) $.
      caucvglem.5 $e |- ( ph -> A. k e. ( ZZ>= ` N ) ( ( F ` k ) e. RR /\
                         ( abs ` ( ( F ` k ) - ( F ` N ) ) ) < ( R / 2 ) ) ) $.
      $( Lemma for ~ caurcvg . $)
      caucvglem $p |- ( ph -> ( A. k e. ( ZZ>= ` N ) ( ( F ` k ) e. CC /\
            ( abs ` ( ( F ` k ) - sup ( S , RR , < ) ) ) < R ) /\
              ( S C_ RR /\ S =/= (/) /\ E. x e. RR A. z e. S z <_ x ) ) ) $=
        ( wcel cr wbr cv cfv cc clt csup cmin co cabs cuz wral wss wne cle wrex
        wa c0 w3a c2 cdiv caddc wb cz eluzelz uzid syl eleq2s simpl ralimi wceq
        fveq2 eleq1d rcla4va syl2anc crab eqsstri a1i crp rphalfcl rpre resubcl
        ssrab2 adantr simpr abslt syl6bi abssub ltsub23 syl3anc 3imtr3d expimpd
        cneg recnd breq1d ralimdv mpd raleqdv rcla4ev breq1 rexralbidv sylanbrc
        elrab2 ne0i ax-addrcl wi rexanuz2 simplbi2 r19.2uz ad2antrr simplr lttr
        absdiflt ltle syld exp3acom23 imp3a a1d rexlimdv syl5 ex syl5d ralrimiv
        raleqi ralrab bitri sylibr breq2 3jca suprcl absdifle suprleub syl22anc
        ralbidv lesubadd mpbird suprub mpbid mpbir2and abscl ltleadd mpan2d jca
        2halves breq2d abs3dif lelttr mpand sylbid jctild ) AHUAZIUBZUCRZUUJFSU
        DUEZUFUGZUHUBZEUDTZUOZHKUIUBZUJZFSUKZFUPULZDUAZBUAZUMTZDFUJZBSUNZUQZAUU
        JSRZUUJKIUBZUFUGZUHUBZEURUSUGZUDTZUOZHUUQUJZUURQAUVMUUPHUUQAUVGUVLUUPAU
        VGUOZUVLUUOUUKUVOUVLUVJUVHUULUFUGZUHUBZUTUGZUVKUVKUTUGZUDTZUUOUVOUVLUVQ
        UVKUMTZUVTAUWAUVGAUWAUULUVKUFUGUVHUMTZUVHUULUVKUTUGUMTZAUVHSRZUULSRZUVK
        SRZUWAUWBUWCUOVAAKUUQRZUVGHUUQUJZUWDAKLRZUWGOUWGKJUIUBZLKUWJRKVBRUWGJKV
        CKVDVEMVFVEAUVNUWHQUVMUVGHUUQUVGUVLVGVHVEUVGUWDHKUUQUUIKVIUUJUVHSUUIKIV
        JVKVLVMZAUVFUWEAUUSUUTUVEUUSAFCUAZUUJUDTZHGUAZUIUBZUJGLUNZCSVNZSNUWPCSW
        AVOVPZAUVHUVKUFUGZFRZUUTAUWSSRZUWSUUJUDTZHUWOUJZGLUNZUWTAUWDUWFUXAUWKAE
        VQRZUWFPUXEUVKVQRUWFEVRUVKVSVEVEZUVHUVKVTVMAUWIUXBHUUQUJZUXDOAUVNUXGQAU
        VMUXBHUUQAUVGUVLUXBUVOUVHUUJUFUGZUHUBZUVKUDTZUXHUVKUDTZUVLUXBUVOUXJUVKW
        KUXHUDTZUXKUOZUXKUVOUXHSRZUWFUXJUXMVAUVOUWDUVGUXNAUWDUVGUWKWBZAUVGWCZUV
        HUUJVTVMAUWFUVGUXFWBZUXHUVKWDVMUXLUXKWCWEUVOUXIUVJUVKUDUVOUVHUCRZUUKUXI
        UVJVIUVOUVHUXOWLZUVOUUJUXPWLZUVHUUJWFVMWMUVOUWDUVGUWFUXKUXBVAUXOUXPUXQU
        VHUUJUVKWGWHWIWJWNWOUXCUXGGKLUWNKVIZUXBHUWOUUQUWNKUIVJZWPWQVMUWPUXDCUWS
        SFUWLUWSVIUWMUXBGHLUWOUWLUWSUUJUDWRWSNXAWTZFUWSXBVEZAUVHUVKUTUGZSRZUVAU
        YEUMTZDFUJZUVEAUWDUWFUYFUWKUXFUVHUVKXCVMZAUVAUUJUDTZHUWOUJGLUNZUYGXDZDS
        UJZUYHAUYLDSAUYKUVMUYJUOZHUWOUJGLUNZUVASRZUYGAUVMHUWOUJZGLUNZUYKUYOXDAU
        WIUVNUYROQUYQUVNGKLUYAUVMHUWOUUQUYBWPWQVMUYOUYRUYKUVMUYJGHJLMXEXFVEAUYP
        UYOUYGXDUYOUYNHLUNAUYPUOZUYGUYNGHJLMXGUYSUYNUYGHLUYSUYNUYGXDUUILRUYSUVM
        UYJUYGUYSUVGUVLUYJUYGXDZUYSUVGUOZUVLUUJUYEUDTZUYTVUAUVLUXBVUBUOZVUBVUAU
        VGUWDUWFUVLVUCVAUYSUVGWCZAUWDUYPUVGUWKXHAUWFUYPUVGUXFXHUUJUVHUVKXKWHUXB
        VUBWCWEVUAUYJVUBUYGVUAUYJVUBUOZUVAUYEUDTZUYGVUAUYPUVGUYFVUEVUFXDAUYPUVG
        XIZVUDAUYFUYPUVGUYIXHZUVAUUJUYEXJWHVUAUYPUYFVUFUYGXDVUGVUHUVAUYEXLVMXMX
        NXMWJXOXPXQXRXSXTYAUYHUYGDUWQUJUYMUYGDFUWQNYBUWPUYKUYGDCSUWLUVAVIUWMUYJ
        GHLUWOUWLUVAUUJUDWRWSYCYDYEZUVDUYHBUYESUVBUYEVIUVCUYGDFUVBUYEUVAUMYFYLW
        QVMYGZBDFYHVEZUXFUVHUULUVKYIWHAUWBUULUYEUMTZAUUSUUTUYFUYHVULUWRUYDUYIVU
        IDFUYEYJYKAUWEUWFUWDUWBVULVAVUKUXFUWKUULUVKUVHYMWHYNAUWSUULUMTZUWCAUVFU
        WTVUMVUJUYCBDFUWSYOVMAUWDUWFUWEVUMUWCVAUWKUXFVUKUVHUVKUULYMWHYPYQWBUVOU
        VJSRZUVQSRZUWFUWFUVLUWAUOUVTXDUVOUVIUCRVUNUVOUVIUVOUVGUWDUVISRUXPUXOUUJ
        UVHVTVMWLUVIYRVEZUVOUVPUCRVUOUVOUVPUVOUWDUWEUVPSRUXOAUWEUVGVUKWBZUVHUUL
        VTVMWLUVPYRVEZUXQUXQUVJUVQUVKUVKYSYKYTUVOUVTUVREUDTZUUOUVOUVSEUVRUDUVOE
        UCRUVSEVIUVOEUVOUXEESRZAUXEUVGPWBEVSVEZWLEUUBVEUUCUVOUUNUVRUMTZVUSUUOUV
        OUUKUULUCRUXRVVBUXTUVOUULVUQWLUXSUUJUULUVHUUDWHUVOUUNSRZUVRSRZVUTVVBVUS
        UOUUOXDUVOUUMUCRVVCUVOUUMUVOUVGUWEUUMSRUXPVUQUUJUULVTVMWLUUMYRVEUVOVUNV
        UOVVDVUPVURUVJUVQXCVMVVAUUNUVREUUEWHUUFUUGXMUXTUUHWJWNWOVUJUUA $.
        $( [15-Feb-2014] $)
    $}

    caurcvg.3 $e |- ( ph -> F e. V ) $.
    caurcvg.4 $e |- ( ph -> A. x e. RR+ E. m e. Z A. k e. ( ZZ>= ` m )
            ( ( F ` k ) e. RR /\ ( abs ` ( ( F ` k ) - ( F ` m ) ) ) < x ) ) $.
    $( A Cauchy sequence of real numbers converges.  The fourth hypothesis
       specifies that ` F ` is a Cauchy sequence. ` S ` is the set of numbers
       less than all values of ` F ` except for finitely many.  Reference:
       Bert G. Wachsmuth,
       ~ http://www.shu.edu/projects/reals/numseq/proofs/cauconv.html .
       _Request:  Please contact Norm Megill if you know of a textbook
       reference for the version of the proof in the link above_.  (Revised by
       Mario Carneiro, 15-Feb-2014.) $)
    caurcvg $p |- ( ph -> F ~~> sup ( S , RR , < ) ) $=
      ( cr wcel cfv wa crp vz vw clt csup cli wbr cc cv cmin cabs cuz wral wrex
      co cz wss c0 wne cle w3a c1 1rp wi wceq biidd c2 cdiv rphalfcl rexralbidv
      breq2 anbi2d rcla4v syl fveq2 eleq1d oveq1d fveq2d breq1d anbi12d cbvralv
      simprl simpl simprr caucvglem simpld expr syl5bir reximdva eluzel2 eleq2s
      sylib syld rexlimiv syl6 simprd rexlimdva jcad vtoclga mpsyl eqidd suprcl
      a1d clim2 recnd com12 ralrimiv mpbir2and ) AHDPUCUDZUEUFXHUGQFUHZHRZUGQXJ
      XHUIUNUJRUAUHZUCUFSFGUHZUKRZULZGKUMZUATULZAUAXHXJGFHIJPKLAIUOQZDPUPDUQURU
      BUHBUHZUSUFUBDULBPUMUTZVATQAXJPQZXJXLHRZUIUNZUJRZXRUCUFZSZFXMULGKUMZBTULZ
      XQXSSZVBOYGYHVCZYIUAVATXKVAVDYIVEXKTQZYGXQXSYJYGXOXQYJYGXTYCXKVFVGUNZUCUF
      ZSZFXMULZGKUMZXOYJYKTQYGYOVCXKVHYFYOBYKTXRYKVDZYEYMGFKXMYPYDYLXTXRYKYCUCV
      JVKVIVLVMZYJYNXNGKYNEUHZHRZPQZYSYAUIUNZUJRZYKUCUFZSZEXMULZYJXLKQZSZXNUUDY
      MEFXMYRXIVDZYTXTUUCYLUUHYSXJPYRXIHVNZVOUUHUUBYCYKUCUUHUUAYBUJUUHYSXJYAUIU
      UIVPVQVRVSVTZYJUUFUUEXNYJUUFUUESZSZXNXSUULBCUBXKDEFHIXLKLMYJUUFUUEWAYJUUK
      WBUULUUEYNYJUUFUUEWCUUJWKWDZWEWFWGWHWLZXNXQGKUUFXQXNXQXLIUKRKIXLWILWJXBWM
      WNYJYGYOXSYQYJYNXSGKYNUUEUUGXSUUJYJUUFUUEXSUULXNXSUUMWOWFWGWPWLWQWRWSZWEN
      AXIKQSXJWTAXSXHPQAXQXSUUOWOBUBDXAVMZXCAXHUUPXDAYGXPOYGXOUATYJYGXOUUNXEXFV
      MXG $.
      $( [15-Feb-2014] $) $( [4-Apr-2005] $)
  $}

  ${
    $d j k m n x y F $.  $d j k m n x M $.  $d k m n ph $.  $d j k m n x y Z $.
    $d j k x G $.  $d j n H $.
    caucvg.1 $e |- Z = ( ZZ>= ` M ) $.
    ${
      caurcvg2.2 $e |- ( ph -> F e. V ) $.
      caurcvg2.3 $e |- ( ph -> A. x e. RR+ E. j e. Z A. k e. ( ZZ>= ` j )
            ( ( F ` k ) e. RR /\ ( abs ` ( ( F ` k ) - ( F ` j ) ) ) < x ) ) $.
      $( A Cauchy sequence of real numbers converges, existence version.
         (Contributed by Mario Carneiro, 15-Feb-2014.) $)
      caurcvg2 $p |- ( ph -> F e. dom ~~> ) $=
        ( vn wcel cv cfv clt wbr wral cr cmin vy vm cuz wrex crab csup cli wceq
        cdm wb fveq2 breq2d cbvralv rexbii a1i rabbiia co cabs wa eleq1d oveq1d
        crp fveq2d breq1d anbi12d oveq2d anbi2d raleqbidv syl5bb cbvrexv ralbii
        sylib caurcvg breldmg syl2anc ) AEGMEUANZDNZEOZPQZDCNZUCOZRZCHUDZUASUEZ
        SPUFZUGQEUGUIMJABUAWDCLUBEFGHIWCVPLNZEOZPQZLWARZCHUDZUASWCWJUJVPSMWBWIC
        HVSWHDLWAVQWFUHZVRWGVPPVQWFEUKZULUMUNUOUPJAVRSMZVRVTEOZTUQZUROZBNZPQZUS
        ZDWARZCHUDZBVBRWGSMZWGUBNZEOZTUQZUROZWQPQZUSZLXCUCOZRZUBHUDZBVBRKXAXKBV
        BWTXJCUBHWTXBWGWNTUQZUROZWQPQZUSZLWARVTXCUHZXJWSXODLWAWKWMXBWRXNWKVRWGS
        WLUTWKWPXMWQPWKWOXLURWKVRWGWNTWLVAVCVDVEUMXPXOXHLWAXIVTXCUCUKXPXNXGXBXP
        XMXFWQPXPXLXEURXPWNXDWGTVTXCEUKVFVCVDVGVHVIVJVKVLVMEWEGUGVNVO $.
        $( [15-Feb-2014] $) $( [4-Apr-2005] $)
    $}

    caucvg.2 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
    caucvg.3 $e |- ( ph -> A. x e. RR+ E. j e. Z A. k e. ( ZZ>= ` j )
                           ( abs ` ( ( F ` k ) - ( F ` j ) ) ) < x ) $.
    ${
      caucvglem2.4 $e |- G = ( n e. Z |-> ( H ` ( F ` n ) ) ) $.
      caucvglem2.5 $e |- H : CC --> RR $.
      caucvglem2.6 $e |- ( ( ( F ` k ) e. CC /\ ( F ` j ) e. CC ) ->
                      ( abs ` ( ( H ` ( F ` k ) ) - ( H ` ( F ` j ) ) ) ) <_
                      ( abs ` ( ( F ` k ) - ( F ` j ) ) ) ) $.
      $( Lemma for ~ caucvg . $)
      caucvglem2 $p |- ( ph -> G e. dom ~~> ) $=
        ( wcel cfv wral wa cvv cv cmpt cuz fvex eqeltri mptex a1i cmin cabs clt
        cc co wbr wrex crp cr ralrimiva wi r19.29uz ex adantr wceq fveq2 eleq1d
        rcla4cva cle simpr simpll syl2anc ffvelrni ad2antrr resubcl recnd abscl
        adantl syl subcl rpre lelttr syl3anc mpand jctild expimpd ralimdv sylan
        ad2antlr an32s reximdva syld fveq2d fvmpt oveq2d breq1d ralbidv rexbiia
        anbi2d rgen eleq1 oveq1 anbi12d ralimi reximi mpan sylbir syl6 ralimdva
        biimpar sylc caurcvg2 ) ABCDGIUAJKGUAQAGEJEUBZFRZHRZUCUANEJXMJIUDRUAKIU
        DUEUFUGUFUHADUBZFRZULQZDJSZXOCUBZFRZUIUMZUJRZBUBZUKUNZDXRUDRZSCJUOZBUPS
        XNGRZUQQZYFXRGRZUIUMZUJRZYBUKUNZTZDYDSZCJUOZBUPSAXPDJLURMXQYEYNBUPXQYBU
        PQZTZYEXOHRZUQQZYQXSHRZUIUMZUJRZYBUKUNZTZDYDSZCJUOZYNYPYEXPYCTZDYDSZCJU
        OZUUEXQYEUUHUSYOXQYEUUHXPYCCDIJKUTVAVBYPUUGUUDCJXQXRJQZYOUUGUUDUSZXQUUI
        TXSULQZYOUUJXPUUKDXRJXNXRVCXOXSULXNXRFVDVEVFUUKYOTZUUFUUCDYDUULXPYCUUCU
        ULXPTZYCUUBYRUUMUUAYAVGUNZYCUUBUUMXPUUKUUNUULXPVHZUUKYOXPVIZPVJUUMUUAUQ
        QZYAUQQZYBUQQZUUNYCTUUBUSUUMYTULQUUQUUMYTUUMYRYSUQQZYTUQQXPYRUULULUQXOH
        OVKVPZUUKUUTYOXPULUQXSHOVKVLYQYSVMVJVNYTVOVQUUMXTULQZUURUUMXPUUKUVBUUOU
        UPXOXSVRVJXTVOVQYOUUSUUKXPYBVSWGUUAYAYBVTWAWBUVAWCWDWEWFWHWIWJUUEYRYQYH
        UIUMZUJRZYBUKUNZTZDYDSZCJUOZYNUVGUUDCJUUIUVFUUCDYDUUIUVEUUBYRUUIUVDUUAY
        BUKUUIUVCYTUJUUIYHYSYQUIEXRXMYSJGXKXRVCXLXSHXKXRFVDWKNXSHUEWLWMWKWNWQWO
        WPYFYQVCZDJSZUVHYNUVIDJEXNXMYQJGXKXNVCXLXOHXKXNFVDWKNXOHUEWLWRUVJUVHTUV
        IUVFTZDYDSZCJUOYNUVIUVFCDIJKUTUVLYMCJUVKYLDYDUVIYLUVFUVIYGYRYKUVEYFYQUQ
        WSUVIYJUVDYBUKUVIYIUVCUJYFYQYHUIWTWKWNXAXHXBXCVQXDXEXFXGXIXJ $.
        $( [15-Feb-2014] $) $( [4-Apr-2005] $)
    $}

    caucvg.4 $e |- ( ph -> F e. V ) $.
    $( A Cauchy sequence of complex numbers converges to a complex number.
       Theorem 12-5.3 of [Gleason] p. 180 (sufficiency part).  (The proof was
       shortened by Mario Carneiro, 15-Feb-2014.) $)
    caucvg $p |- ( ph -> F e. dom ~~> ) $=
      ( vn wcel cfv cli ci co cvv cc cv cre cmpt cim cmul wbr cdm cmin cabs clt
      caddc cuz wral wrex crp cz c0 wne c1 1rp ne0i ax-mp r19.2z sylancr eleq2s
      eluzel2 a1d rexlimiv rexlimivw syl fvex a1i ref wa cle resub fveq2d subcl
      eqid absrele eqbrtrrd caucvglem2 climdm sylib ovex absimle ax-icn eqeltri
      imf imsub mptex wceq fveq2 fvmpt adantl cr recnd eqeltrd oveq2d climmulc2
      imcl eqtr4d recl mulcl replim oveq12d climadd breldmg syl2anc ) AEGNEMHMU
      AZEOZUBOZUCZPOZQMHXKUDOZUCZPOZUERZUKRZPUFEPUGZNLAXNXRDXMMHQXOUERZUCZEFSSG
      HIADUAZEOZCUAZEOZUHRZUIOZBUAUJUFDYEULOUMZCHUNZBUOUNZFUPNZAUOUQURZYJBUOUMY
      KUSUONYMUTUOUSVAVBKYJBUOVCVDYJYLBUOYIYLCHYEHNYLYIYLYEFULOZHFYEVFIVEVGVHVI
      VJZXNSNAXMPVKVLAXMXTNXMXNPUFABCDMEXMUBFHIJKXMVSZVMYDTNZYFTNVNZYGUBOZUIOZY
      DUBOZYFUBOUHRZUIOYHVOYRYSUUBUIYDYFVPVQYRYGTNZYTYHVOUFYDYFVRZYGVTVJWAWBXMW
      CWDXRSNAQXQUEWEVLLAXQQDXPYBFSSHIYOXQSNAXPPVKVLAXPXTNXPXQPUFABCDMEXPUDFHIJ
      KXPVSZWIYRYGUDOZUIOZYDUDOZYFUDOUHRZUIOYHVOYRUUFUUIUIYDYFWJVQYRUUCUUGYHVOU
      FUUDYGWFVJWAWBXPWCWDQTNZAWGVLYBSNAMHYAHYNSIFULVKWHWKVLAYCHNZVNZYCXPOZUUHT
      UUKUUMUUHWLAMYCXOUUHHXPXJYCWLZXKYDUDXJYCEWMZVQZUUEYDUDVKWNWOZUULUUHUULYQU
      UHWPNJYDXAVJWQZWRUULYCYBOZQUUHUERZQUUMUERUUKUUSUUTWLAMYCYAUUTHYBUUNXOUUHQ
      UEUUPWSYBVSQUUHUEWEWNWOZUULUUMUUHQUEUUQWSXBWTUULYCXMOZUUATUUKUVBUUAWLAMYC
      XLUUAHXMUUNXKYDUBUUOVQYPYDUBVKWNWOZUULUUAUULYQUUAWPNJYDXCVJWQWRUULUUSUUTT
      UVAUULUUJUUHTNUUTTNWGUURQUUHXDVDWRUULYDUUAUUTUKRZUVBUUSUKRUULYQYDUVDWLJYD
      XEVJUULUVBUUAUUSUUTUKUVCUVAXFXBXGEXSGPXHXI $.
      $( [15-Feb-2014] $) $( [20-Dec-2006] $)
  $}

  ${
    $d j k m n x F $.  $d j k m n x M $.  $d j k n x ph $.  $d j k m n x Z $.
    $d k m n V $.
    caucvgb.1 $e |- Z = ( ZZ>= ` M ) $.
    $( A function is convergent if and only if it is Cauchy.  Theorem 12-5.3 of
       [Gleason] p. 180.  (Contributed by Mario Carneiro, 15-Feb-2014.) $)
    caucvgb $p |- ( ( M e. ZZ /\ F e. V ) -> ( F e. dom ~~> <->
              A. x e. RR+ E. j e. Z A. k e. ( ZZ>= ` j ) ( ( F ` k ) e. CC /\
                ( abs ` ( ( F ` k ) - ( F ` j ) ) ) < x ) ) ) $=
      ( vn vm wcel wa cv cfv wral wrex cli crp c1 ralimi cz cc cuz cmin co cabs
      cdm clt wbr cop wex eldm2g ibi df-br cvv simpll 1rp a1i eqidd simpr climi
      vex simpl reximi ex syl5bir exlimdv syl5 wi wb wceq fveq2 raleqdv cbvrexv
      syl rcla4v mpsyl eluzelz eleq2s eqid climcau sylan r19.29uz ralimdv mpan9
      an32s adantll simplrr eleq1d rcla4cva oveq1d fveq2d breq1d cbvralv adantl
      sylib caucvg adantlll impbida ad2antrl bitr4d exp32 rexlimdv pm5.21ndd
      cau4 ) EUAKZDFKZLZCMZDNZUBKZCIMZUCNZOZIGPZDQUGZKZXKXJBMZDNZUDUEZUFNZAMZUH
      UIZLZCXRUCNZOZBGPZAROZXQDJMZUJQKZJUKZXHXOXQYKJDQXPULUMXHYJXOJYJDYIQUIZXHX
      ODYIQUNXHYLXOXHYLLZXKXJYIUDUEUFNSUHUIZLZCXMOZIGPXOYMYIXJSICDEUOGHXFXGYLUP
      SRKZYMUQURYMXIGKLXJUSXHYLUTYIUOKYMJVBURVAYPXNIGYOXKCXMXKYNVCTVDVOVEVFVGVH
      YHXOVIXHYQYHXKCYEOZBGPZAROXOUQYGYSARYFYRBGYDXKCYEXKYCVCTVDTYSXOASRYSXOVJY
      BSVKYRXNBIGXRXLVKXKCYEXMXRXLUCVLVMVNURVPVQURXHXNXQYHVJZIGXHXLGKZXNYTXHUUA
      XNLZLZXQYFBXMPZAROZYHUUCXQUUEUUBXQUUEXHUUAXQXNUUEUUAXQLYCCYEOZBXMPZAROZXN
      UUEUUAXLUAKZXQUUHUUIXLEUCNGEXLVRHVSABCDXLXMXMVTZWAWBXNUUGUUDARXNUUGUUDXKY
      CBCXLXMUUJWCVEWDWEWFWGXGUUBUUEXQXFXGUUBLZUUELZABJDXLFXMUUJUULXNYIXMKYIDNZ
      UBKZXGUUAXNUUEWHXKUUNCYIXMXIYIVKZXJUUMUBXIYIDVLZWIWJWBUUEUUMXSUDUEZUFNZYB
      UHUIZJYEOZBXMPZAROUUKUUDUVAARYFUUTBXMYFUUFUUTYDYCCYEXKYCUTTYCUUSCJYEUUOYA
      UURYBUHUUOXTUUQUFUUOXJUUMXSUDUUPWKWLWMWNWPVDTWOXGUUBUUEUPWQWRWSUUAYHUUEVJ
      XHXNABCDEXLXMGHUUJXEWTXAXBXCXD $.
      $( [15-Feb-2014] $)

    serf0.2 $e |- ( ph -> M e. ZZ ) $.
    serf0.3 $e |- ( ph -> F e. V ) $.
    serf0.4 $e |- ( ph -> seq M ( + , F ) e. dom ~~> ) $.
    serf0.5 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
    $( If an infinite series converges, its underlying sequence converges to
       zero.  (Revised by Mario Carneiro, 16-Feb-2014.) $)
    serf0 $p |- ( ph -> F ~~> 0 ) $=
      ( vx vm cfv cabs wral cc wcel cmin co vn vj cc0 cli wbr cv clt wrex caddc
      cuz crp cseq wa cdm cz wb caucvgb syl2anc mpbid sylib c1 peano2uzs adantl
      cau3 wi eluzelz uzid peano2uz wceq fveq2 oveq2d fveq2d breq1d rcla4v 3syl
      adantld ralimia simpr syl6eleq eluzp1m1 sylan oveq1 oveq12d serf ad2antrr
      syl wf uztrn2 syldan ffvelrn abssub zcn npcan sylancl eluzp1p1 eqid seqm1
      ax-1cn oveq1d adantlr pncan2 eqtr2d 3eqtr4d sylibd ralrimdva syl5 raleqdv
      rcla4ev ex sylsyld rexlimdva ralimdv mpd eqidd clim0c mpbird ) ACUCUDUEBU
      FZCNZONZLUFZUGUEZBUAUFZUJNZPZUAFUHZLUKPZAMUFZUICDULZNZQRZYIXQYHNZSTZONZXT
      UGUEZBYGUJNZPZUMZMUBUFZUJNZPZUBFUHZLUKPZYFAYJYIYRYHNSTONXTUGUEUMMYSPUBFUH
      LUKPZUUBAYHUDUNZRZUUCJADUORZUUEUUEUUCUPHJLUBMYHDUUDFGUQURUSLUBMBYHDFGVDUT
      AUUAYELUKAYTYEUBFAYRFRZUMZYRVAUITZFRZYTYABUUIUJNZPZYEUUGUUJADYRFGVBVCZYTY
      IYGVAUITZYHNZSTZONZXTUGUEZMYSPZUUHUULYQUURMYSYGYSRZYPUURYJUUTYGUORYGYORZY
      PUURVEZYRYGVFYGVGUVAUUNYORUVBYGYGVHYNUURBUUNYOXQUUNVIZYMUUQXTUGUVCYLUUPOU
      VCYKUUOYISXQUUNYHVJVKVLVMVNWFVOVPVQUUHUUSYABUUKUUHXQUUKRZUMZUUSXQVASTZYHN
      ZUVFVAUITZYHNZSTZONZXTUGUEZYAUVEUVFYSRZUUSUVLVEUUHYRUORZUVDUVMUUHYRDUJNZR
      ZUVNUUHYRFUVOAUUGVRZGVSZDYRVFWFYRXQVTWAZUURUVLMUVFYSYGUVFVIZUUQUVKXTUGUVT
      UUPUVJOUVTYIUVGUUOUVISYGUVFYHVJUVTUUNUVHYHYGUVFVAUIWBVLWCVLVMVNWFUVEUVKXS
      XTUGUVEUVGYKSTZONZYKUVGSTZONZUVKXSUVEUVGQRZYKQRZUWBUWDVIUVEFQYHWGZUVFFRZU
      WEAUWGUUGUVDABCDFGHKWDWEZUUHUVDUVMUWHUVSUUHUUGUVMUWHUVQDUVFYRFGWHWAWIFQUV
      FYHWJURZUVEUWGXQFRZUWFUWIUUHUUJUVDUWKUUMDXQUUIFGWHWAZFQXQYHWJURUVGYKWKURU
      VEUVJUWAOUVEUVIYKUVGSUVEUVHXQYHUVEXQQRZVAQRUVHXQVIUVEXQUORZUWMUVDUWNUUHUU
      IXQVFVCXQWLWFWRXQVAWMWNVLVKVLUVEXRUWCOUVEUWCUVGXRUITZUVGSTZXRUVEYKUWOUVGS
      UVEUUFXQDVAUITZUJNZRZYKUWOVIAUUFUUGUVDHWEUUHUUIUWRRZUVDUWSUUHUVPUWTUVRDYR
      WOWFUWQXQUUIUWRUWRWPWHWAUICDXQWQURWSUVEUWEXRQRZUWPXRVIUWJUUHUVDUWKUXAUWLA
      UWKUXAUUGKWTWIUVGXRXAURXBVLXCVMXDXEXFUUJUULYEYDUULUAUUIFYBUUIVIYABYCUUKYB
      UUIUJVJXGXHXIXJXKXLXMALXRUABCDEFGHIAUWKUMXRXNKXOXP $.
      $( [16-Feb-2014] $) $( [2-Sep-2005] $)
  $}

