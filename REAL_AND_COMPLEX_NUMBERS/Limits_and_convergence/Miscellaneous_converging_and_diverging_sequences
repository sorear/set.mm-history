$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Miscellaneous converging and diverging sequences
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d j k n x A $.
    $( The sequence of reciprocals of natural numbers, multiplied by the factor
       ` A ` , converges to zero.  (Revised by Mario Carneiro, 26-Apr-2014.) $)
    divcnv $p |- ( A e. CC -> ( n e. NN |-> ( A / n ) ) ~~> 0 ) $=
      ( vk vx vj cc wcel cn cv cdiv co cc0 cmul c1 wbr clt crp cr wa adantl cli
      cmpt cvv nnuz cz 1z a1i 0cn cabs cfv cuz wral wrex elrp nnrecl cle eluzle
      sylbi wb simplr uztrn2 adantll nnre nngt0 lerec syl2an syl2anc mpbid nnrp
      jca wi rpreccl 3syl rpre syl nnrecre ad2antrr lelttr syl3anc wceq rprege0
      mpand absid breq1d ralrimdva reximdva mpd rgen nnex mptex oveq2 eqid ovex
      sylibrd fvmpt recnd clim0c mpbiri id eqeltrd wne simpl nncn divrec oveq2d
      nnne0 3eqtr4d climmulc2 mul01 breqtrd ) AFGZBHABIZJKZUBZALMKLUAXKLACBHNXL
      JKZUBZXNNFUCHUDNUEGXKUFUGZLFGXKUHUGXKXPLUAONCIZJKZUIUJZDIZPOZCEIZUKUJZULZ
      EHUMZDQULYFDQYAQGZNYCJKZYAPOZEHUMZYFYGYARGZLYAPOSYJYAUNYAEUOURYGYIYEEHYGY
      CHGZSZYIYBCYDYMXRYDGZSZYIXSYAPOZYBYOXSYHUPOZYIYPYOYCXRUPOZYQYNYRYMYCXRUQT
      YOYLXRHGZYRYQUSZYGYLYNUTZYLYNYSYGNXRYCHUDVAVBZYLYCRGZLYCPOZSXRRGZLXRPOZSY
      TYSYLUUCUUDYCVCYCVDVJYSUUEUUFXRVCXRVDVJYCXRVEVFVGVHYOXSRGZYHRGZYKYQYISYPV
      KYOXSQGZUUGYOYSXRQGUUIUUBXRVIXRVLVMZXSVNVOYOYLUUHUUAYCVPVOYGYKYLYNYAVNVQX
      SYHYAVRVSWBYOXTXSYAPYOUUIUUGLXSUPOSXTXSVTUUJXSWAXSWCVMWDWNWEWFWGWHXKDXSEC
      XPNUCHUDXQXPUCGXKBHXOWIWJUGYSXRXPUJZXSVTXKBXRXOXSHXPXLXRNJWKXPWLNXRJWMWOT
      ZYSXSFGXKYSXSXRVPWPTZWQWRXKWSXNUCGXKBHXMWIWJUGXKYSSZUUKXSFUULUUMWTUUNAXRJ
      KZAXSMKZXRXNUJZAUUKMKUUNXKXRFGZXRLXAZUUOUUPVTXKYSXBYSUURXKXRXCTYSUUSXKXRX
      FTAXRXDVSYSUUQUUOVTXKBXRXMUUOHXNXLXRAJWKXNWLAXRJWMWOTUUNUUKXSAMUULXEXGXHA
      XIXJ $.
      $( [26-Apr-2014] $) $( [6-Feb-2008] $)
  $}

  ${
    $d f k m x z F $.  $d f k m n z ph $.  $d f k m x z R $.  $d f k m x z X $.
    $d x y z A $.  $d k m n S $.
    supcvg.1 $e |- X e. _V $.
    supcvg.2 $e |- S = sup ( A , RR , < ) $.
    supcvg.3 $e |- R = ( n e. NN |-> ( S - ( 1 / n ) ) ) $.
    supcvg.4 $e |- ( ph -> X =/= (/) ) $.
    supcvg.5 $e |- ( ph -> F : X -onto-> A ) $.
    supcvg.6 $e |- ( ph -> A C_ RR ) $.
    supcvg.7 $e |- ( ph -> E. x e. RR A. y e. A y <_ x ) $.
    $( Extract a sequence ` f ` in ` X ` such that the image of the points in
       the bounded set ` A ` converges to the supremum ` S ` of the set.
       Similar to Equation 4 of [Kreyszig] p. 144.  (Contributed by Mario
       Carneiro, 15-Feb-2013.)  (The proof was shortened by Mario Carneiro,
       26-Apr-2014.) $)
    supcvg $p |- ( ph -> E. f ( f : NN --> X /\ ( F o. f ) ~~> S ) ) $=
      ( cn wcel cr vk vz vm cv wf cfv cle wbr wral wa wex ccom cli wrex csup c1
      clt cdiv cmin wceq oveq2 oveq2d ovex fvmpt adantl crp wss wne w3a wfo fof
      syl feq3 syl5ibcom f00 simprbi syl6 necon3d mpd 3jca suprcl syl5eqel nnrp
      co rpreccl ltsubrp syl2an eqbrtrd syl6breq adantr nnrecre resubcl ffvelrn
      c0 wb fmptd sylan suprlub syl2anc mpbid sselda ltle reximdva forn rexeqdv
      wi crn wfn ffn breq2 rexrn bitr3d ralrimiva nnenom fveq2 breq2d axcc4 cvv
      nnuz cz 1z a1i ad2antrr cc0 csn cxp cmpt cc recnd cuz eqimss2i climconst2
      nnex sylancl 0z mptex eqeltri ax-1cn eqeltrd ad3antrrr divcnv eqid eqtr4d
      fvconst2g oveq12d climsub subid1 breqtrd fex vex coexg fss fveq2d breq12d
      fco rcla4cva adantll wfun ffun simpllr simpr fvco3 breqtrrd simplr syldan
      syl3anc suprub syl6breqr climsqueeze ex imdistanda eximdv ) ARJGUDZUEZUAU
      DZEUFZUVOUVMUFZIUFZUGUHZUARUIZUJZGUKZUVNIUVMULZFUMUHZUJZGUKAUVPBUDZIUFZUG
      UHZBJUNZUARUIUWBAUWIUARAUVORSZUJZUVPUBUDZUGUHZUBDUNZUWIUWKUVPUWLUQUHZUBDU
      NZUWNUWKUVPDTUQUOZUQUHZUWPUWKUVPFUWQUQUWKUVPFUPUVOURWDZUSWDZFUQUWJUVPUWTU
      TAHUVOFUPHUDZURWDZUSWDZUWTREUXAUVOUTUXBUWSFUSUXAUVOUPURVAZVBMFUWSUSVCVDVE
      ZAFTSZUWSVFSZUWTFUQUHUWJAFUWQTLADTVGZDWNVHZCUDUWFUGUHCDUIBTUNZVIZUWQTSAUX
      HUXIUXJPAJWNVHUXINADWNJWNADWNUTZJWNIUEZJWNUTZAJDIUEZUXLUXMAJDIVJZUXOOJDIV
      KVLZDWNJIVMVNUXMIWNUTUXNJIVOVPVQVRVSQVTZBCDWAVLWBZUWJUVOVFSUXGUVOWCUVOWEV
      LFUWSWFWGWHLWIUWKUXKUVPTSZUWRUWPWOAUXKUWJUXRWJARTEUEZUWJUXTAHRUXCTEAUXFUX
      BTSUXCTSUXARSUXSUXAWKFUXBWLWGMWPZRTUVOEWMWQZBCUBDUVPWRWSWTUWKUWOUWMUBDUWK
      UWLDSZUJUXTUWLTSUWOUWMXFUWKUXTUYDUYCWJUWKDTUWLAUXHUWJPWJXAUVPUWLXBWSXCVSA
      UWNUWIWOUWJAUWMUBIXGZUNZUWNUWIAUWMUBUYEDAUXPUYEDUTOJDIXDVLXEAIJXHZUYFUWIW
      OAUXOUYGUXQJDIXIVLUWMUWHUBBJIUWLUWGUVPUGXJXKVLXLWJWTXMUWHUVSBJGUARKXNUWFU
      VQUTUWGUVRUVPUGUWFUVQIXOXPXQVLAUWAUWEGAUVNUVTUWDAUVNUJZUVTUWDUYHUVTUJZFUC
      EUWCUPTXRRXSUPXTSZUYIYAYBAUXFUVNUVTUXSYCAEFUMUHUVNUVTAEFYDUSWDZFUMAFYDUAR
      FYEYFZHRUXBYGZEUPTXTXRRXSUYJAYAYBUXSAFYHSZUYJUYLFUMUHAFUXSYIZYAFUPRRUPYJU
      FXSYKYMYLYNYDXTSAYOYBEXRSAEHRUXCYGXRMHRUXCYMYPYQYBAUPYHSZUYMYDUMUHUYPAYRY
      BUPHUUAVLUWKUVOUYLUFZFYHAUXFUWJUYQFUTUXSRFUVOTUUDWQZAUYNUWJUYOWJYSUWKUVOU
      YMUFZUWSYHUWJUYSUWSUTAHUVOUXBUWSRUYMUXDUYMUUBUPUVOURVCVDVEZUWJUWSYHSAUWJU
      WSUVOWKYIVEYSUWKUVPUWTUYQUYSUSWDUXEUWKUYQFUYSUWSUSUYRUYTUUEUUCUUFAUYNUYKF
      UTUYOFUUGVLUUHYCUYIIXRSZUVMXRSUWCXRSUYIUXOJXRSVUAAUXOUVNUVTUXQYCZKJDXRIUU
      IYNGUUJIUVMXRXRUUKYNUYIUYAUCUDZRSZVUCEUFZTSAUYAUVNUVTUYBYCRTVUCEWMWQUYIRT
      UWCUEZVUDVUCUWCUFZTSUYHVUFUVTAJTIUEZUVNVUFAUXOUXHVUHUXQPJDTIUULWSRJTIUVMU
      UOWQWJRTVUCUWCWMWQUYIVUDUJZVUEVUCUVMUFZIUFZVUGUGUVTVUDVUEVUKUGUHZUYHUVSVU
      LUAVUCRUVOVUCUTZUVPVUEUVRVUKUGUVOVUCEXOVUMUVQVUJIUVOVUCUVMXOUUMUUNUUPUUQV
      UIIUURZUVNVUDVUGVUKUTAVUNUVNUVTVUDAUXOVUNUXQJDIUUSVLYTAUVNUVTVUDUUTUYIVUD
      UVARJVUCIUVMUVBUVFZUVCVUIVUGVUKFUGVUOVUIVUKUWQFUGVUIUXKVUKDSZVUKUWQUGUHAU
      XKUVNUVTVUDUXRYTUYIVUDVUJJSZVUPUYIUVNVUDVUQAUVNUVTUVDRJVUCUVMWMWQUYIUXOVU
      QVUPVUBJDVUJIWMWQUVEBCDVUKUVGWSLUVHWHUVIUVJUVKUVLVS $.
      $( [26-Apr-2014] $) $( [15-Feb-2013] $)
  $}

  ${
    $d u x A $.  $d x y B $.  $d w z R $.  $d x y X $.  $d u x y Z $.
    $d y C $.
    infcvg.1 $e |- R = { x | E. y e. X x = -u A } $.
    infcvg.2 $e |- ( y e. X -> A e. RR ) $.
    infcvg.3 $e |- Z e. X $.
    infcvg.4 $e |- E. z e. RR A. w e. R w <_ z $.
    $( Auxiliary theorem for applications of ~ supcvg .  Hypothesis for several
       supremum theorems. $)
    infcvgaux1i $p |- ( R C_ RR /\ R =/= (/)
           /\ E. z e. RR A. w e. R w <_ z ) $=
      ( vu cr cv wrex cneg wceq wcel ax-17 wss wne cle wbr wral cab renegcl syl
      c0 eleq1 syl5ibrcom rexlimiv abssi eqsstri csb eqid csbeq1a negeqd eqeq2d
      hbth rcla4e mp2an negex elexi hbcsb1 hbneg hbeq eqeq1 elab mpbir eleqtrri
      rexbid ne0i ax-mp 3pm3.2i ) FNUAFUIUBZDOCOUCUDDFUECNPFAOZEQZRZBGPZAUFZNIV
      TANVSVQNSZBGBOZGSZWBVSVRNSZWDENSWEJEUGUHVQVRNUJUKULUMUNBHEUOZQZFSVPWGWAFW
      GWASWGVRRZBGPZHGSWGWGRZWIKWGUPZWHWJBHGWJBWKUTWCHRZVRWGWGWLEWFBHEUQURUSVAV
      BVTWIAWGWFVCVQWGRVSWHBGBMMVQWGMOZVQSBTBMWFBMHEHGKVDWMHSBTVEVFVGVQWGVRVHVL
      VIVJIVKFWGVMVNLVO $.
      $( [8-Feb-2008] $)

    ${
      infcvg.5a $e |- S = -u sup ( R , RR , < ) $.
      infcvg.13 $e |- ( y = C -> A = B ) $.
      $( Auxiliary theorem for applications of ~ supcvg . $)
      infcvgaux2i $p |- ( C e. X -> S <_ B ) $=
        ( wcel cr wceq clt csup cneg cle wbr wrex eqid cv negeqd eqeq2d rcla4ev
        mpan2 negex eqeq1 rexbidv elab2 sylibr infcvgaux1i suprubii syl vtoclga
        wb eleq1d suprclii lenegcon1 sylancl mpbid syl5eqbr ) GJRZIHSUAUBZUCZFU
        DPVIFUCZVJUDUEZVKFUDUEZVIVLHRZVMVIVLEUCZTZBJUFZVOVIVLVLTZVRVLUGVQVSBGJB
        UHGTZVPVLVLVTEFQUIUJUKULAUHZVPTZBJUFVRAVLHFUMWAVLTWBVQBJWAVLVPUNUOLUPUQ
        CDHVLABCDEHJKLMNOURZUSUTVIFSRZVJSRVMVNVBESRWDBGJVTEFSQVCMVACDHWCVDFVJVE
        VFVGVH $.
        $( [4-Mar-2008] $)
    $}
  $}

  ${
    $d k n $.  $d j k F $.  $d j k H $.
    harmonic.1 $e |- F = ( n e. NN |-> ( 1 / n ) ) $.
    harmonic.2 $e |- H = seq 1 ( + , F ) $.
    $( The harmonic series ` H ` diverges.  This fact follows from the stronger
       ~ emcl , which establishes that the harmonic series grows as
       ` log n + gamma + ` _o_(1), but this uses a more elementary method,
       attributed to Nicole Oresme (1323-1382).  (Contributed by Mario
       Carneiro, 11-Jul-2014.) $)
    harmonic $p |- -. H e. dom ~~> $=
      ( vk wcel cn0 c1 wbr cn cr cc0 wceq adantl cdiv co cle syl cmul vj cli cv
      cdm csu clt wrex csn cxp nn0uz cz 0z a1i cfv 1z elexi fvconst2 caddc cseq
      1re eleq1i biimpi oveq2 ovex fvmpt nnrecre eqeltrd crp nnrp rpreccl rpge0
      wa 3syl breqtrrd nnre lep1 nngt0 peano2re peano2nn lerec syl22anc 3brtr4d
      wb mpbid c2 cexp fveq2d oveq12d cmpt fconstmpt 2nn nnexpcl mpan oveq2d cc
      wne nncn nnne0 recid syl2anc eqtrd mpteq2ia eqtr4i climcnds isumrecl arch
      cfz chash cfn fzfid ax-1cn fsumconst sylancl hashfz1 oveq1d mulid1 3eqtrd
      wn nnnn0 wss elfznn ssriv 0re lt01 ltleii isumless eqbrtrrd lenlt syl2anr
      adantr nrexdv pm2.65i ) CUBUDZGZHIFUEZUAUCZUFJZUAKUGZYNYOLGZYRYNIFHIUHUIZ
      MHUJMUKGZYNULUMFUCZHGZUUBYTUNINZYNHIUUBIUKUOUPUQZOILGZYNUUCVLUTUMYNURBIUS
      ZYMGZURYTMUSYMGZYNUUHCUUGYMEVAVBYNFUABYTUUBKGZUUBBUNZLGYNUUJUUKIUUBPQZLAU
      UBIAUCZPQZUULKBUUMUUBIPVCDIUUBPVDVEZUUBVFVGOUUJMUUKRJYNUUJMUULUUKRUUJUUBV
      HGUULVHGMUULRJUUBVIUUBVJUULVKVMUUOVNOUUJUUBIURQZBUNZUUKRJYNUUJIUUPPQZUULU
      UQUUKRUUJUUBUUPRJZUURUULRJZUUJUUBLGZUUSUUBVOZUUBVPSUUJUVAMUUBUFJUUPLGZMUU
      PUFJZUUSUUTWCUVBUUBVQUUJUVAUVCUVBUUBVRSUUJUUPKGZUVDUUBVSZUUPVQSUUBUUPVTWA
      WDUUJUVEUUQUURNUVFAUUPUUNUURKBUUMUUPIPVCDIUUPPVDVESUUOWBOYPHGZYPYTUNWEYPW
      FQZUVHBUNZTQZNYNFYPWEUUBWFQZUVKBUNZTQZUVJHYTUUBYPNZUVKUVHUVLUVITUUBYPWEWF
      VCZUVNUVKUVHBUVOWGWHYTFHIWIFHUVMWIFHIWJFHUVMIUUCUVMUVKIUVKPQZTQZIUUCUVLUV
      PUVKTUUCUVKKGZUVLUVPNWEKGUUCUVRWKWEUUBWLWMZAUVKUUNUVPKBUUMUVKIPVCDIUVKPVD
      VESWNUUCUVRUVQINZUVSUVRUVKWOGUVKMWPUVTUVKWQUVKWRUVKWSWTSXAXBXCUVHUVITVDVE
      OXDWDZXEZYOUAXFSYNYQUAKYNYPKGZVLZYPYORJZYQXRZUWDIYPXGQZIFUEZYPYORUWDUWHUW
      GXHUNZITQZYPITQZYPUWDUWGXIGIWOGUWHUWJNUWDIYPXJZXKUWGIFXLXMUWDUWIYPITUWDUV
      GUWIYPNUWCUVGYNYPXSOYPXNSXOUWDYPWOGZUWKYPNUWCUWMYNYPWQOYPXPSXQUWDUWGIFYTM
      HUJUUAUWDULUMUWLUWGHXTUWDFUWGHUUBUWGGUUJUUCUUBYPYAUUBXSSYBUMUUCUUDUWDUUEO
      UUFUWDUUCVLZUTUMMIRJUWNMIYCUTYDYEUMYNUUIUWCUWAYJYFYGUWCYPLGYSUWEUWFWCYNYP
      VOUWBYPYOYHYIWDYKYL $.
      $( [11-Jul-2014] $)
  $}


