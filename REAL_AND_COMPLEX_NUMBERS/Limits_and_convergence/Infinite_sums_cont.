$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Infinite sums (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d k m n x A $.  $d j k m n x K $.  $d j k m n x ph $.  $d j k m n x W $.
    $d j m n x B $.  $d m n x M $.  $d k m n x Z $.
    isumshft.1 $e |- Z = ( ZZ>= ` M ) $.
    isumshft.2 $e |- W = ( ZZ>= ` ( M + K ) ) $.
    isumshft.3 $e |- ( j = ( K + k ) -> A = B ) $.
    isumshft.4 $e |- ( ph -> K e. ZZ ) $.
    isumshft.5 $e |- ( ph -> M e. ZZ ) $.
    isumshft.6 $e |- ( ( ph /\ j e. W ) -> A e. CC ) $.
    $( Index shift of an infinite sum.  (Contributed by Paul Chapman,
       31-Oct-2007.)  (Revised by Mario Carneiro, 24-Apr-2014.) $)
    isumshft $p |- ( ph -> sum_ j e. W A = sum_ k e. Z B ) $=
      ( vx cfv caddc wcel wceq vm vn cv cmpt csu co cseq cli wbr cshi cz zaddcl
      cio syl2anc cuz eleq2i wa cmin cc zcn syl eluzelz eleq2s cvv fvex eqeltri
      mptex shftval syl2an wral cid simpr eqid fvmpt2i syl6eleq eluzadd syl2anr
      addcom id eqeltrd syl6eleqr fvmpti eqtr4d ralrimiva ax-17 hbfv hbeq fveq2
      hbmpt1 oveq2 fveq2d eqeq12d rcla4 adantr eluzsub syl3anc rcla4cva 3eqtrrd
      mpan9 pncan3 sylan2br seqfeq breq1d wb vex isershft bitr4d iotabidv seqex
      a1i fv4 3eqtr4g eqidd wf fmptd ffvelrn sylan eleq1d 3eqtr4d sumfc 3eqtr3g
      isum ) AHUAUCZDHBUDZQZUAUEZIUBUCZEICUDZQZUBUEZHBDUEICEUEARYDGFRUFZUGZUHQZ
      RYHGUGZUHQZYFYJAYLPUCZUHUIZPUMYNYPUHUIZPUMYMYOAYQYRPAYQRYHFUJUFZYKUGZYPUH
      UIZYRAYLYTYPUHARUAYDYSYKAGUKSZFUKSZYKUKSNMGFULUNZYCYKUOQZSZAYCHSZYEYCYSQZ
      THUUEYCKUPAUUGUQZUUHYCFURUFZYHQZFUUJRUFZYDQZYEAFUSSZYCUSSZUUHUUKTUUGAUUCU
      UNMFUTVAZUUOYCUUEHUUFYCUKSUUOYKYCVBYCUTVAKVCZFYCYHEICIGUOQZVDJGUOVEVFVGZV
      HVIUUIYIFYGRUFZYDQZTZUBIVJZUUJISUUKUUMTZAUVCUUGAUVBUBIAEUCZYHQZFUVERUFZYD
      QZTZEIVJYGISZUVBAUVIEIAUVEISZUQZUVFCVKQZUVHUVLUVKUVFUVMTAUVKVLEICYHYHVMVN
      VAUVLUVGHSZUVHUVMTUVLUVGUUEHUVLUVGUVEFRUFZUUEAUUNUVEUSSZUVGUVOTUVKUUPUVPU
      VEUURIUVEUURSZUVEUKSUVPGUVEVBUVEUTVAJVCFUVEVRVIUVKUVQUUCUVOUUESAUVKUVEIUU
      RUVKVSJVOMFGUVEVPVQVTKWAZDUVGBCHYDLYDVMZWBVAWCWDUVIUVBEYGIEPPYIUVAEPYGYHE
      PICWIYPYGSEWEWFYPUVASEWEWGUVEYGTZUVFYIUVHUVAUVEYGYHWHUVTUVGUUTYDUVEYGFRWJ
      ZWKWLWMWSZWDWNUUIUUJUURIUUIUUBUUCUUFUUJUURSAUUBUUGNWNAUUCUUGMWNUUIYCHUUEA
      UUGVLKVOFGYCWOWPJWAUVBUVDUBUUJIYGUUJTZYIUUKUVAUUMYGUUJYHWHUWCUUTUULYDYGUU
      JFRWJWKWLWQUNUUIUULYCYDAUUNUUOUULYCTUUGUUPUUQFYCWTVIWKWRXAXBXCAUUBUUCYPVD
      SZYRUUAXDNMUWDAPXEXJYPRYHGFVDUUSXFWPXGXHPYLUHRYDYKXIXKPYNUHRYHGXIXKXLAYEU
      AYDYKHKUUDUUIYEXMAHUSYDXNZUUGYEUSSADHBUSYDOUVSXOZHUSYCYDXPXQYBAYIUBYHGIJN
      AUVJUQZYIXMUWGYIUVAUSUWBUWGUWEUUTHSZUVAUSSAUWEUVJUWFWNAUVNEIVJUVJUWHAUVNE
      IUVRWDUVNUWHEYGIUVTUVGUUTHUWAXRWQXQHUSUUTYDXPUNVTYBXSHBUADXTICUBEXTYA $.
      $( [24-Apr-2014] $) $( [31-Oct-2007] $)
  $}

  ${
    $d j A $.  $d j k m x F $.  $d j k m x M $.  $d j k m x ph $.  $d k Z $.
    $d j k m x N $.  $d j k m x W $.
    isumsplit.1 $e |- Z = ( ZZ>= ` M ) $.
    isumsplit.2 $e |- W = ( ZZ>= ` N ) $.
    isumsplit.3 $e |- ( ph -> N e. Z ) $.
    isumsplit.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = A ) $.
    isumsplit.5 $e |- ( ( ph /\ k e. Z ) -> A e. CC ) $.
    isumsplit.6 $e |- ( ph -> seq M ( + , F ) e. dom ~~> ) $.
    $( Split off the first ` N ` terms of an infinite sum.  (Contributed by
       Paul Chapman, 9-Feb-2007.)  (Revised by Mario Carneiro, 24-Apr-2014.) $)
    isumsplit $p |- ( ph -> sum_ k e. Z A =
                      ( sum_ k e. ( M ... ( N - 1 ) ) A + sum_ k e. W A ) ) $=
      ( co caddc cfv wcel wceq cc vj vm vx c1 cmin cfz csu cvv syl6eleq eluzel2
      cuz cz syl ovex a1i cseq cli cdm eluzelz sumex wss uzss 3sstr4g sseld imp
      cv syldan wa eqeltrd iserex mpbid isumclim2 fzfid elfzuz syl6eleqr sylan2
      fsumcl wf serf ffvelrn sylan wo uzp1 adantr cc0 c0 clt wbr cr zre ltm1 wb
      3syl peano2zm fzn syl2anc sumeq1d sum0 syl6eq oveq1d addid2 eqtr2d oveq2d
      oveq1 seqeq1 fveq1d oveq12d eqeq2d syl5ibrcom adantl addass simplr simpll
      addcl w3a zcn ax-1cn sylancl eqcomd fveq2d syl5eq eleqtrd eluzp1m1 syl2an
      npcan seqsplit fsumser seqeq1d eqtr4d ex jaod mpd climaddc2 isumclim ) AB
      EFUDUEOZUFOZBCUGZGBCUGZPOZCDEUHHIAFEUKQZRZEULRZAFHYTKIUIZEFUJUMZLMYSUHRAY
      QYRPUNUOAYRYQUAPDFUPZPDEUPZFUHUQURZGJAUUAFULRZUUCEFUSUMZYRUHRAGBCUTUOABCD
      FGJUUIACVFZGRZUUJHRZUUJDQZBSZAUUKUULAGHUUJAFUKQZYTGHAUUAUUOYTVAUUCEFVBUMJ
      IVCZVDVEZLVGAUUKUULBTRZUUQMVGAUUFUUGRUUEUUGRNACDEFHIKAUULVHUUMBTLMVIZVJVK
      VLAYPBCAEYOVMUUJYPRZAUULUURUUTUUJYTHUUJEYOVNIVOZMVPVQNAGTUUEVRUAVFZGRZUVB
      UUEQZTRACDFGJUUIAUUKUULUUMTRZUUQUUSVGVSGTUVBUUEVTWAAUVCVHZFESZFEUDPOUKQRZ
      WBZUVBUUFQZYQUVDPOZSZAUVIUVCAUUAUVIUUCEFWCUMWDUVFUVGUVLUVHUVFUVLUVGUVJEEU
      DUEOZUFOZBCUGZUVJPOZSUVFUVPWEUVJPOZUVJUVFUVOWEUVJPUVFUVOWFBCUGZWEAUVOUVRS
      UVCAUVNWFBCAUVMEWGWHZUVNWFSZAUUBEWIRUVSUUDEWJEWKWMAUUBUVMULRZUVSUVTWLUUDA
      UUBUWAUUDEWNUMEUVMWOWPVKWQWDBCWRWSWTUVFUVJTRZUVQUVJSAUVCUVBHRZUWBAUVCUWCA
      GHUVBUUPVDVEAHTUUFVRUWCUWBACDEHIUUDUUSVSHTUVBUUFVTWAVGUVJXAUMXBUVGUVKUVPU
      VJUVGYQUVOUVDUVJPUVGYPUVNBCUVGYOUVMEUFFEUDUEXDXCWQUVGUVBUUEUUFPDFEXEXFXGX
      HXIUVFUVHUVLUVFUVHVHZUVJYOUUFQZUVBPDYOUDPOZUPZQZPOUVKUWDCUBUCPTDEYOUVBUUJ
      TRZUBVFZTRZVHUUJUWJPOZTRUWDUUJUWJXNXJUWIUWKUCVFZTRXOUWLUWMPOUUJUWJUWMPOPO
      SUWDUUJUWJUWMXKXJUWDUVBGUWFUKQZAUVCUVHXLUWDGUUOUWNJUWDFUWFUKUWDAFUWFSAUVC
      UVHXMZAUWFFAFTRZUDTRUWFFSAUUHUWPUUIFXPUMXQFUDYEXRXSUMZXTYAYBUVFUUBUVHYOYT
      RAUUBUVCUUDWDEFYCWAZUWDAUULUVEUUJEUVBUFORZUWOUWSUUJYTHUUJEUVBVNIVOUUSYDYF
      UWDYQUWEUVDUWHPUWDBCDEYOUWDAUULUUNUUTUWOUVALYDUWRUWDAUULUURUUTUWOUVAMYDYG
      UWDUVBUUEUWGUWDFUWFPDUWQYHXFXGYIYJYKYLYMYN $.
      $( [24-Apr-2014] $) $( [9-Feb-2008] $)
  $}

  ${
    $d k F $.  $d k M $.  $d k ph $.  $d k Z $.
    isum1p.1 $e |- Z = ( ZZ>= ` M ) $.
    isum1p.3 $e |- ( ph -> M e. ZZ ) $.
    isum1p.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = A ) $.
    isum1p.5 $e |- ( ( ph /\ k e. Z ) -> A e. CC ) $.
    isum1p.6 $e |- ( ph -> seq M ( + , F ) e. dom ~~> ) $.
    $( The infinite sum of a converging infinite series equals the first term
       plus the infinite sum of the rest of it.  (Revised by Mario Carneiro,
       24-Apr-2014.) $)
    isum1p $p |- ( ph -> sum_ k e. Z A =
                   ( ( F ` M ) + sum_ k e. ( ZZ>= ` ( M + 1 ) ) A ) ) $=
      ( csu c1 caddc co cfz cfv wcel cc wceq cmin cuz eqid cz uzid syl peano2uz
      syl6eleqr isumsplit zcn ax-1cn pncan sylancl oveq2d sumeq1d elfzuz sylan2
      cv sumeq2dv wral eqeltrd ralrimiva fveq2 eleq1d rcla4v sylc fsum1 syl2anc
      wa 3eqtr2d oveq1d eqtrd ) AFBCLEEMNOZMUAOZPOZBCLZVMUBQZBCLZNOEDQZVRNOABCD
      EVMVQFGVQUCAVMEUBQZFAEVTRZVMVTRAEUDRZWAHEUEUFZEEUGUFGUHIJKUIAVPVSVRNAVPEE
      POZBCLWDCURZDQZCLZVSAVOWDBCAVNEEPAESRZMSRVNETAWBWHHEUJUFUKEMULUMUNUOAWDWF
      BCWEWDRZAWEFRZWFBTWIWEVTFWEEEUPGUHIUQUSAWBVSSRZWGVSTHAEFRWFSRZCFUTWKAEVTF
      WCGUHAWLCFAWJVIWFBSIJVAVBWLWKCEFWEETWFVSSWEEDVCZVDVEVFWFVSCEWMVGVHVJVKVL
      $.
      $( [24-Apr-2014] $) $( [2-Jan-2006] $)
  $}

  ${
    $d k F $.  $d k B $.  $d k ph $.
    isumnn0nn.1 $e |- ( k = 0 -> A = B ) $.
    isumnn0nn.2 $e |- ( ( ph /\ k e. NN0 ) -> ( F ` k ) = A ) $.
    isumnn0nn.3 $e |- ( ( ph /\ k e. NN0 ) -> A e. CC ) $.
    isumnn0nn.4 $e |- ( ph -> seq 0 ( + , F ) e. dom ~~> ) $.
    $( Sum from 0 to infinity in terms of sum from 1 to infinity.  (Revised by
       Mario Carneiro, 24-Apr-2014.) $)
    isumnn0nn $p |- ( ph -> sum_ k e. NN0 A = ( B + sum_ k e. NN A ) ) $=
      ( cn0 csu cc0 cfv c1 caddc co cuz cn wcel wceq nn0uz cz 0z isum1p cv wral
      a1i 0nn0 ralrimiva fveq2 eqeq12d rcla4v ax-1cn addid2i fveq2i nnuz eqtr4i
      mpsyl sumeq1i oveq12d eqtrd ) AJBDKLEMZLNOPZQMZBDKZOPCRBDKZOPABDELJUALUBS
      AUCUGGHIUDAVBCVEVFOLJSADUEZEMZBTZDJUFVBCTZUHAVIDJGUIVIVJDLJVGLTVHVBBCVGLE
      UJFUKULURVEVFTAVDRBDVDNQMRVCNQNUMUNUOUPUQUSUGUTVA $.
      $( [24-Apr-2014] $) $( [2-Jan-2006] $)
  $}

  ${
    $d m A $.  $d k m F $.  $d k M $.  $d k m N $.  $d k m ph $.  $d k m W $.
    $d k Z $.
    isumrpcl.1 $e |- Z = ( ZZ>= ` M ) $.
    isumrpcl.2 $e |- W = ( ZZ>= ` N ) $.
    isumrpcl.3 $e |- ( ph -> N e. Z ) $.
    isumrpcl.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = A ) $.
    isumrpcl.5 $e |- ( ( ph /\ k e. Z ) -> A e. RR+ ) $.
    isumrpcl.6 $e |- ( ph -> seq M ( + , F ) e. dom ~~> ) $.
    $( The infinite sum of positive reals is positive.  (Contributed by Paul
       Chapman, 9-Feb-2008.)  (Revised by Mario Carneiro, 24-Apr-2014.) $)
    isumrpcl $p |- ( ph -> sum_ k e. W A e. RR+ ) $=
      ( cr wcel cc0 crp cfv syl vm csu clt wbr cuz cz syl6eleq eluzelz wceq wss
      cv uzss 3sstr4g sseld imp syldan wa rpre caddc cseq cli cdm eqeltrd recnd
      iserex mpbid isumrecl 0re a1i wral ralrimiva fveq2 eleq1d rcla4v sylc cle
      rpgt0 seq1 uzid syl6eleqr isumclim2 syl5com syld rpge0 climserle eqbrtrrd
      ltletrd elrp sylanbrc ) AGBCUBZOPQWJUCUDWJRPABCDFGJAFEUESZPZFUFPZAFHWKKIU
      GZEFUHTZACUKZGPZWPHPZWPDSZBUIAWQWRAGHWPAFUESZWKGHAWLWTWKUJWNEFULTJIUMZUNU
      OZLUPZAWQWRBOPZXBAWRUQZBRPXDMBURTUPZAUSDEUTVAVBZPUSDFUTZXGPNACDEFHIKXEWSX
      EWSRPZWSOPXEWSBRLMVCZWSURTVDVEVFZVGZAQFDSZWJQOPAVHVIAXMRPZXMOPAFHPXICHVJZ
      XNKAXICHXJVKZXIXNCFHWPFUIWSXMRWPFDVLVMVNVOZXMURTXLAXNQXMUCUDXQXMVQTAFXHSZ
      XMWJVPAWMXRXMUIWOUSDFVRTAWJUADFFGJAFWTGAWMFWTPWOFVSTJVTABCDFGJWOXCAWQUQBX
      FVDXKWAAUAUKZGPZUQZXSDSZRPZYBOPAXTYCAXTXSHPZYCAGHXSXAUNAXOYDYCXPXIYCCXSHW
      PXSUIWSYBRWPXSDVLVMVNWBWCUOZYBURTYAYCQYBVPUDYEYBWDTWEWFWGWJWHWI $.
      $( [24-Apr-2014] $) $( [9-Feb-2008] $)
  $}

  ${
    $d k F $.  $d k G $.  $d k M $.  $d k ph $.  $d k Z $.
    isumle.1 $e |- Z = ( ZZ>= ` M ) $.
    isumle.2 $e |- ( ph -> M e. ZZ ) $.
    isumle.3 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = A ) $.
    isumle.4 $e |- ( ( ph /\ k e. Z ) -> A e. RR ) $.
    isumle.5 $e |- ( ( ph /\ k e. Z ) -> ( G ` k ) = B ) $.
    isumle.6 $e |- ( ( ph /\ k e. Z ) -> B e. RR ) $.
    isumle.7 $e |- ( ( ph /\ k e. Z ) -> A <_ B ) $.
    isumle.8 $e |- ( ph -> seq M ( + , F ) e. dom ~~> ) $.
    isumle.9 $e |- ( ph -> seq M ( + , G ) e. dom ~~> ) $.
    $( Comparison of two infinite sums.  (Contributed by Paul Chapman,
       13-Nov-2007.)  (Revised by Mario Carneiro, 24-Apr-2014.) $)
    isumle $p |- ( ph -> sum_ k e. Z A <_ sum_ k e. Z B ) $=
      ( cli cfv wcel caddc cseq csu cle cvv fvex a1i cdm wbr climdm sylib cv wa
      cr eqeltrd 3brtr4d iserle recnd isum ) AUAEGUBZRSZUAFGUBZRSZHBDUCHCDUCUDA
      VAVCDEFGUEHIJVAUETAUTRUFUGAUTRUHZTUTVARUIPUTUJUKAVBVDTVBVCRUIQVBUJUKADULZ
      HTUMZVEESZBUNKLUOVFVEFSZCUNMNUOVFBCVGVHUDOKMUPUQABDEGHIJKVFBLURUSACDFGHIJ
      MVFCNURUSUP $.
      $( [24-Apr-2014] $) $( [13-Nov-2007] $)
  $}

  ${
    $d j k A $.  $d j k F $.  $d k M $.  $d k ph $.  $d j k Z $.
    isumless.1 $e |- Z = ( ZZ>= ` M ) $.
    isumless.2 $e |- ( ph -> M e. ZZ ) $.
    isumless.3 $e |- ( ph -> A e. Fin ) $.
    isumless.4 $e |- ( ph -> A C_ Z ) $.
    isumless.5 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = B ) $.
    isumless.6 $e |- ( ( ph /\ k e. Z ) -> B e. RR ) $.
    isumless.7 $e |- ( ( ph /\ k e. Z ) -> 0 <_ B ) $.
    isumless.8 $e |- ( ph -> seq M ( + , F ) e. dom ~~> ) $.
    $( A finite sum of nonnegative numbers is less or equal to its limit.
       (Contributed by Mario Carneiro, 24-Apr-2014.) $)
    isumless $p |- ( ph -> sum_ k e. A B <_ sum_ k e. Z B ) $=
      ( wcel cc0 cle cfv cr vj csu cv cif wss cc wral cuz cfn wo wceq sseld imp
      recnd syldan ralrimiva eqimssi orci a1i sumss2 syl21anc eleq1 fveq2 eqidd
      wa cmpt ifbieq12d eqid fvex 0re elexi ifex fvmpt adantl ifeq1d eqtrd ifcl
      sylancl wbr leid breq1 ifboth sylan syl2anc fsumcvg3 isumle eqbrtrd ) ABC
      DUBZGDUCZBPZCQUDZDUBZGCDUBRABGUECUFPZDBUGGFUHSZUEZGUIPZUJZWHWLUKKAWMDBAWJ
      WIGPZWMAWJWRABGWIKULUMAWRVEZCMUNUOZUPWQAWOWPGWNHUQURUSBGCDFUTVAAWKCDUAGUA
      UCZBPZXAESZQUDZVFZEFGHIWSWIXESZWJWIESZQUDZWKWRXFXHUKAUAWIXDXHGXEXAWIUKZXB
      WJXCQXGQXAWIBVBXAWIEVCXIQVDVGXEVHWJXGQWIEVIQTVJVKVLVMVNWSWJXGCQLVOVPZWSCT
      PZQTPWKTPMVJWJCQTVQVRLMWSXKQCRVSZWKCRVSZMNXKCCRVSZXLXMCVTWJXNXLXMCQCWKCRW
      AQWKCRWAWBWCWDABCDXEFGHIJKXJWTWEOWFWG $.
      $( [24-Apr-2014] $)
  $}

  ${
    $d j x A $.  $d j k x F $.  $d j k x M $.  $d j k ph $.  $d j k x Z $.
    $d j x G $.
    isumsup.1 $e |- Z = ( ZZ>= ` M ) $.
    isumsup.2 $e |- G = seq M ( + , F ) $.
    isumsup.3 $e |- ( ph -> M e. ZZ ) $.
    isumsup.4 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = A ) $.
    isumsup.5 $e |- ( ( ph /\ k e. Z ) -> A e. RR ) $.
    isumsup.6 $e |- ( ( ph /\ k e. Z ) -> 0 <_ A ) $.
    isumsup.7 $e |- ( ph -> E. x e. RR A. j e. Z ( G ` j ) <_ x ) $.
    $( An infinite sum of nonnegative terms is equal to the supremum of the
       partial sums.  (Contributed by Mario Carneiro, 12-Jun-2014.) $)
    isumsup2 $p |- ( ph -> G ~~> sup ( ran G , RR , < ) ) $=
      ( cr wcel cfv co caddc cseq wf cv wa eqeltrd serfre feq1i sylibr c1 simpr
      cle cuz syl6eleq cz eluzelz syl uzid peano2uz 3syl simpl elfzuz syl6eleqr
      cfz syl2an cc0 wbr peano2uzs adantl uztrn2 breqtrrd adantlr syldan fveq1i
      sermono 3brtr4g climsup ) ABDGHIJLAIQUAFHUBZUCIQGUCAEFHIJLAEUDZIRZUEZVSFS
      ZCQMNUFZUGIQGVRKUHUIADUDZIRZUEZWDVRSWDUJUATZVRSWDGSWGGSULWFEFWDHWGWFWDIHU
      MSZAWEUKJUNZWFWDUORZWDWDUMSZRWGWKRWFWDWHRWJWIHWDUPUQWDURWDWDUSUTWFAVTWBQR
      VSHWGVDTRZAWEVAWLVSWHIVSHWGVBJVCWCVEWFVSWGWGVDTRZVTVFWBULVGZWFWGIRZVSWGUM
      SRVTWMWEWOAHWDIJVHVIVSWGWGVBHVSWGIJVJVEAVTWNWEWAVFCWBULOMVKVLVMVOWDGVRKVN
      WGGVRKVNVPPVQ $.
      $( [12-Jun-2014] $)

    $( An infinite sum of nonnegative terms is equal to the supremum of the
       partial sums.  (Contributed by Mario Carneiro, 12-Jun-2014.) $)
    isumsup $p |- ( ph -> sum_ k e. Z A = sup ( ran G , RR , < ) ) $=
      ( cr clt cvv wcel crn csup cv wa recnd ltso supex a1i caddc cseq isumsup2
      cli syl5eqbrr isumclim ) ACGUAZQRUBZEFHSIJLMAEUCITUDCNUEUPSTAQUORUFUGUHAU
      IFHUJGUPULKABCDEFGHIJKLMNOPUKUMUN $.
      $( [12-Jun-2014] $)
  $}

  ${
    $d k x A $.  $d x B $.  $d k F $.  $d k x M $.  $d k x ph $.  $d k x Z $.
    isumltss.1 $e |- Z = ( ZZ>= ` M ) $.
    isumltss.2 $e |- ( ph -> M e. ZZ ) $.
    isumltss.3 $e |- ( ph -> A e. Fin ) $.
    isumltss.4 $e |- ( ph -> A C_ Z ) $.
    isumltss.5 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) = B ) $.
    isumltss.6 $e |- ( ( ph /\ k e. Z ) -> B e. RR+ ) $.
    isumltss.7 $e |- ( ph -> seq M ( + , F ) e. dom ~~> ) $.
    $( A partial sum of a series with positive terms is less than the infinite
       sum.  (Contributed by Jeff Madsen, 2-Sep-2009.)  (The proof was
       shortened by Mario Carneiro, 3-Jun-2014.) $)
    isumltss $p |- ( ph -> sum_ k e. A B < sum_ k e. Z B ) $=
      ( vx wcel wbr wceq cfn com cv cdif wex csu clt c0 wn ominf cuz cfv cvv wb
      cen fvex cz cc0 cif fveq2 breq2d c1 caddc cmpt crdg cres wf1o elimel eqid
      co om2uzf1oi omex f1oen ax-mp dedth syl enfi sylancr eleq1i syl6bbr mtbii
      0z wss ssdif0 wa eqss eleq1 syl5ibcom syl5bir mpand mtod sylib csn adantr
      neq0 cun cr ssel2 sylan crp adantlr rpre syldan fsumrecl snfi unfi eldifi
      sylancl snssd anim12i unss cseq cli cdm isumrecl a1i snnz adantl fsumrpcl
      wne vex ltaddrp syl2anc cin eldifn disjsn sylibr eqidd cc recnd fsumsplit
      breqtrrd cle rpge0 isumless ltletrd ex exlimdv mpd ) AOUAZGBUBZPZOUCZBCDU
      DZGCDUDZUEQZAYSUFRZUGUUAAUUEGSPZATSPZUUFUHAUUGFUIUJZSPZUUFAUUHUKPTUUHUMQZ
      UUGUUIULFUIUNAFUOPZUUJIUUKUUJTUUKFUPUQZUIUJZUMQZFUPFUULRUUHUUMTUMFUULUIUR
      USTUUMOUKYRUTVAVHVBUULVCTVDZVEUUNOUULUUOFUPUOVTVFUUOVGVITUUMUUOVJVKVLVMVN
      TUUHUKVOVPGUUHSHVQVRVSUUEGBWAZAUUFGBWBABGWAZUUPUUFKUUQUUPWCBGRZAUUFBGWDAB
      SPZUURUUFJBGSWEWFWGWHWGWIOYSWMWJAYTUUDOAYTUUDAYTWCZUUBBYRWKZWNZCDUDZUUCUU
      TBCDAUUSYTJWLZUUTDUAZBPZUVEGPZCWOPZUUTUUQUVFUVGAUUQYTKWLBGUVEWPWQUUTUVGWC
      ZCWRPZUVHAUVGUVJYTMWSZCWTVNZXAXBZUUTUVBCDUUTUUSUVASPZUVBSPUVDYRXCZBUVAXDX
      FZUUTUVEUVBPZUVGUVHUUTUVBGWAZUVQUVGUUTUUQUVAGWAZWCUVRAUUQYTUVSKYTYRGYRGBX
      EXGZXHBUVAGXIWJZUVBGUVEWPWQZUVLXAXBUUTCDEFGHAUUKYTIWLZAUVGUVEEUJCRYTLWSZU
      VLAVAEFXJXKXLPYTNWLZXMUUTUUBUUBUVACDUDZVAVHZUVCUEUUTUUBWOPUWFWRPUUBUWGUEQ
      UVMUUTUVACDUVNUUTUVOXNUVAUFXRUUTYROXSXOXNUUTUVEUVAPZUVGUVJUUTUVSUWHUVGYTU
      VSAUVTXPUVAGUVEWPWQUVKXAXQUUBUWFXTYAUUTBUVACUVBDUUTYRBPUGZBUVAYBUFRYTUWIA
      YRGBYCXPBYRYDYEUUTUVBYFUVPUUTUVQUVGCYGPUWBUVICUVLYHXAYIYJUUTUVBCDEFGHUWCU
      VPUWAUWDUVLUVIUVJUPCYKQUVKCYLVNUWEYMYNYOYPYQ $.
      $( [3-Jun-2014] $) $( [2-Sep-2009] $)
  $}

