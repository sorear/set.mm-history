$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Superior limit (lim sup)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c limsup $.

  $( Extend class notation to include the limsup function. $)
  clsp $a class limsup $.

  ${
    $d k x F $.  $d x G $.  $d x M $.  $d x N $.
$(
    df-limsupOLD $a |- limsup = { <. x , y >. | y = sup ( { z |
                  E. k e. ZZ ( ran ( x |` ( ZZ>= ` k ) ) C_ RR* /\
        z = sup ( ran ( x |` ( ZZ>= ` k ) ) , RR* , < ) ) } , RR* , `' < ) } $.

    df-limsupOLD2 $a |- limsup = { <. x , y >. | y = sup ( { z |
   E. k e. ZZ z = sup ( ( x " ( ZZ>= ` k ) ) , RR* , < ) } , RR* , `' < ) } $.
$)

    $( Define the superior limit of an infinite sequence of extended real
       numbers.  Definition 12-4.1 of [Gleason] p. 175.  See ~ limsupval for
       its value. $)
    df-limsup $a |- limsup = ( x e. _V |-> sup ( ran ( k e. ZZ |->
      sup ( ( ( x " ( ZZ>= ` k ) ) i^i RR* ) , RR* , < ) ) , RR* , `' < ) ) $.

    $( Ordering property of the superior limit function.  (Contributed by Mario
       Carneiro, 7-Sep-2014.) $)
    limsupgord $p |- ( N e. ( ZZ>= ` M ) ->
      sup ( ( ( F " ( ZZ>= ` N ) ) i^i RR* ) , RR* , < ) <_
      sup ( ( ( F " ( ZZ>= ` M ) ) i^i RR* ) , RR* , < ) ) $=
      ( vx cuz cfv wcel cima cxr cin clt csup cle wbr wss inss2 ax-mp supxrleub
      wral wb cv uzss imass2 ssrin 3syl supxrcl xrleid mp2an mpbi ssralv sylibr
      ee10 ) CBEFZGZDUAAUMHZIJZIKLZMNZDACEFZHZIJZSZVAIKLUQMNZUNVAUPOZURDUPSZVBU
      NUSUMOUTUOOVDBCUBUSUMAUCUTUOIUDUEUQUQMNZVEUQIGZVFUPIOZVGUOIPZUPUFQZUQUGQV
      HVGVFVETVIVJDUPUQRUHUIURDVAUPUJULVAIOVGVCVBTUTIPVJDVAUQRUHUK $.
      $( [7-Sep-2014] $)

    limsupval.1 $e |- G = ( k e. ZZ |->
      sup ( ( ( F " ( ZZ>= ` k ) ) i^i RR* ) , RR* , < ) ) $.
    $( The superior limit of an infinite sequence ` F ` of extended real
       numbers, which is the infimum (indicated by ` ``' < ` ) of the set of
       suprema of all upper infinite subsequences of ` F ` .  Definition 12-4.1
       of [Gleason] p. 175. $)
    limsupval $p |- ( F e. V ->
      ( limsup ` F ) = sup ( ran G , RR* , `' < ) ) $=
      ( vx wcel cvv clsp cfv crn cxr clt csup wceq cz cv cima cin cmpt ccnv cuz
      elex imaeq1 ineq1d supeq1d mpteq2dv rneqd df-limsup wor xrltso cnvso mpbi
      syl6eqr supex fvmpt syl ) BDGBHGBIJCKZLMUAZNZOBDUCFBAPFQZAQUBJZRZLSZLMNZT
      ZKZLUSNUTHIVABOZLVGURUSVHVFCVHVFAPBVBRZLSZLMNZTCVHAPVEVKVHLVDVJMVHVCVILVA
      BVBUDUEUFUGEUNUHUFFAUILURUSLMUJLUSUJUKLMULUMUOUPUQ $.
      $( [5-Sep-2014] $) $( [26-Oct-2005] $)
  $}

  ${
    $d x F $.
    $( Closure of the superior limit. $)
    limsupcl $p |- ( F e. V -> ( limsup ` F ) e. RR* ) $=
      ( vx wcel clsp cfv cuz cima cxr cin clt csup cmpt crn ccnv eqid limsupval
      cz cv wss wral inss2 supxrcl ax-mp rgenw fmpt mpbi infmxrcl mp2b syl6eqel
      wf frn ) ABDAEFCRACSGFHZIJZIKLZMZNZIKOLZICAUPBUPPZQRIUPUKZUQITURIDUOIDZCR
      UAUTVACRUNITVAUMIUBUNUCUDUECRIUOUPUSUFUGRIUPULUQUHUIUJ $.
      $( [7-Sep-2014] $) $( [26-Oct-2005] $)
  $}

  ${
    $d j x A $.  $d j k n x F $.  $d j n x G $.  $d j k n M $.  $d j k n x N $.
    $d j x B $.  $d j V $.  $d j k n x Z $.
    limsupg.1 $e |- G = ( k e. Z |->
      sup ( ( ( F " ( ZZ>= ` k ) ) i^i RR* ) , RR* , < ) ) $.
    $( Closure of the superior limit function.  (Contributed by Mario Carneiro,
       7-Sep-2014.) $)
    limsupgf $p |- G : Z --> RR* $=
      ( cv cuz cfv cima cxr cin clt csup wcel wral wf wss inss2 supxrcl ax-mp
      rgenw fmpt mpbi ) BAFGHIZJKZJLMZJNZADODJCPUGADUEJQUGUDJRUESTUAADJUFCEUBUC
      $.
      $( [7-Sep-2014] $)

    $( Value of the superior limit function.  (Contributed by Mario Carneiro,
       7-Sep-2014.) $)
    limsupgval $p |- ( M e. Z -> ( G ` M ) =
      sup ( ( ( F " ( ZZ>= ` M ) ) i^i RR* ) , RR* , < ) ) $=
      ( cuz cfv cima cxr cin clt csup wceq fveq2 imaeq2d ineq1d supeq1d xrltso
      cv supex fvmpt ) ADBATZGHZIZJKZJLMBDGHZIZJKZJLMECUCDNZJUFUILUJUEUHJUJUDUG
      BUCDGOPQRFJUILSUAUB $.
      $( [7-Sep-2014] $)

    limsupg.2 $e |- Z = ( ZZ>= ` M ) $.
    $( The superior limit, relativized to a set of upper integers.
       (Contributed by Mario Carneiro, 7-Sep-2014.) $)
    limsupval2 $p |- ( ( F e. V /\ M e. ZZ ) ->
      ( limsup ` F ) = sup ( ran G , RR* , `' < ) ) $=
      ( vx wcel cz cfv cxr clt csup wceq cle wbr wss ax-mp vn clsp cuz cima cin
      cmpt crn ccnv cvv elex imaeq1 ineq1d supeq1d mpteq2dv rneqd df-limsup wor
      cv xrltso cnvso mpbi supex fvmpt syl wral cres uzssz eqsstri resmpt resss
      eqtr4i rnss wf inss2 supxrcl rgenw eqid frn infmxrcl xrleid wb infmxrgelb
      fmpt mp2an ssralv mp2 limsupgf mpbir a1i wa cif wfn ffn inss1 simpr simpl
      ifcl syl2anc uzid eleqtrrd syl6eleqr fnfvelrn sylancr infmxrlb limsupgval
      sseldi limsupgord eqbrtrd wi ffvelrni xrletr mp3an13 mp2and fveq2 imaeq2d
      adantl breqtrrd ralrimiva breq2 ralrn sylibr xrletri3 sylanbrc sylan9eq
      uzin ) BEJZDKJZBUBLZAKBAURZUCLZUDZMUEZMNOZUFZUGZMNUHZOZCUGZMYPOZYFBUIJYHY
      QPBEUJIBAKIURZYJUDZMUEZMNOZUFZUGZMYPOYQUIUBYTBPZMUUEYOYPUUFUUDYNUUFAKUUCY
      MUUFMUUBYLNUUFUUAYKMYTBYJUKULUMUNUOUMIAUPMYOYPMNUQMYPUQUSMNUTVAVBVCVDYGYQ
      YSQRZYSYQQRZYQYSPZUUGYGUUGYQYTQRZIYRVEZYRYOSZUUJIYOVEZUUKCYNSUULCYNFVFZYN
      CAFYMUFZUUNGFKSUUNUUOPFDUCLZKHDVGVHAKFYMVITVKYNFVJVHCYNVLTYQYQQRZUUMYQMJZ
      UUQYOMSZUURKMYNVMZUUSYMMJZAKVEUUTUVAAKYLMSUVAYKMVNYLVOTVPAKMYMYNYNVQZWCVA
      ZKMYNVRTZYOVSTZYQVTTUUSUURUUQUUMWAUVDUVEIYOYQWBWDVAUUJIYRYOWEWFYRMSZUURUU
      GUUKWAFMCVMZUVFABCFGWGZFMCVRTZUVEIYRYQWBWDWHWIYGYSYTQRZIYOVEZUUHYGYSUAURZ
      YNLZQRZUAKVEZUVKYGUVNUAKYGUVLKJZWJZYSBUVLUCLZUDZMUEZMNOZUVMQUVQYSDUVLQRZU
      VLDWKZCLZQRZUWDUWAQRZYSUWAQRZUVQUVFUWDYRJZUWEUVIUVQCFWLZUWCFJZUWHUVGUWIUV
      HFMCWMTUVQUWCUUPFUVQUUPUVRUEZUUPUWCUUPUVRWNUVQUWCUWCUCLZUWKUVQUWCKJZUWCUW
      LJUVQUVPYGUWMYGUVPWOZYGUVPWPZUWBUVLDKWQWRUWCWSVDUVQYGUVPUWKUWLPUWOUWNDUVL
      YEWRWTZXFHXAZFUWCCXBXCYRUWDXDXCUVQUWDBUWLUDMUEMNOZUWAQUVQUWJUWDUWRPUWQABC
      UWCFGXEVDUVQUWCUVRJUWRUWAQRUVQUWKUVRUWCUUPUVRVNUWPXFBUVLUWCXGVDXHUVQUWDMJ
      ZUWEUWFWJUWGXIZUVQUWJUWSUWQFMUWCCUVHXJVDYSMJZUWSUWAMJZUWTUVFUXAUVIYRVSTZU
      VTMSUXBUVSMVNUVTVOTYSUWDUWAXKXLVDXMUVPUVMUWAPYGAUVLYMUWAKYNYIUVLPZMYLUVTN
      UXDYKUVSMUXDYJUVRBYIUVLUCXNXOULUMUVBMUVTNUSVBVCXPXQXRYNKWLZUVKUVOWAUUTUXE
      UVCKMYNWMTUVJUVNIUAKYNYTUVMYSQXSXTTYAUUSUXAUUHUVKWAUVDUXCIYOYSWBWDYAUURUX
      AUUIUUGUUHWJWAUVEUXCYQYSYBWDYCYD $.
      $( [7-Sep-2014] $)

    $( The defining property of the superior limit function. $)
    limsupgle $p |- ( ( ( N e. Z /\ Z C_ B /\ F : B --> RR* ) /\ A e. RR* ) ->
      ( ( G ` N ) <_ A <-> A. j e. ( ZZ>= ` N ) ( F ` j ) <_ A ) ) $=
      ( vx wcel wss cxr cfv cle wbr wral syl wf w3a wa cuz cima cin clt csup cv
      wceq simpl1 limsupgval breq1d wb inss2 simpr supxrleub sylancr crn simpl3
      imassrn frn syl5ss df-ss sylib raleqdv wfn syl6eleq uzss syl6sseqr simpl2
      ffn sstrd breq1 raleqfn syl2anc bitr4d 3bitrd ) HIMZIBNZBOEUAZUBZAOMZUCZH
      FPZAQREHUDPZUEZOUFZOUGUHZAQRZLUIZAQRZLWHSZCUIEPZAQRZCWFSZWDWEWIAQWDVSWEWI
      UJVSVTWAWCUKZDEFHIJULTUMWDWHONWCWJWMUNWGOUOWBWCUPLWHAUQURWDWMWLLWGSZWPWDW
      LLWHWGWDWGONWHWGUJWDWGEUSZOEWFVAWDWAWSONVSVTWAWCUTZBOEVBTVCWGOVDVEVFWDEBV
      GZWFBNWPWRUNWDWAXAWTBOEVLTWDWFIBWDWFGUDPZIWDHXBMWFXBNWDHIXBWQKVHGHVITKVJV
      SVTWAWCVKVMWLWOCLBWFEWKWNAQVNVOVPVQVR $.
      $( [5-Sep-2014] $)

    $( The defining property of the superior limit.  (Contributed by Mario
       Carneiro, 7-Sep-2014.) $)
    limsuple $p |- ( ( ( M e. ZZ /\ Z C_ B /\ F : B --> RR* ) /\
      B e. V /\ A e. RR* ) ->
      ( A <_ ( limsup ` F ) <-> A. j e. Z A <_ ( G ` j ) ) ) $=
      ( vx wcel wss cxr wf w3a cle wbr ax-mp cz clsp cfv crn clt ccnv csup wral
      cv cvv wceq simp13 simp2 fex syl2anc simp11 limsupval2 breq2d wb limsupgf
      frn simp3 infmxrgelb sylancr wfn ffn breq2 ralrn syl6bb bitrd ) GUAMZIBNZ
      BOEPZQZBHMZAOMZQZAEUBUCZRSAFUDZOUEUFUGZRSZACUIFUCZRSZCIUHZVQVRVTARVQEUJMZ
      VKVRVTUKVQVMVOWEVKVLVMVOVPULVNVOVPUMBOHEUNUOVKVLVMVOVPUPDEFGUJIJKUQUOURVQ
      WAALUIZRSZLVSUHZWDVQVSONZVPWAWHUSIOFPZWIDEFIJUTZIOFVATVNVOVPVBLVSAVCVDFIV
      EZWHWDUSWJWLWKIOFVFTWGWCLCIFWFWBARVGVHTVIVJ $.
      $( [7-Sep-2014] $)

    $( The defining property of the superior limit.  (Contributed by Mario
       Carneiro, 7-Sep-2014.) $)
    limsuplt $p |- ( ( ( M e. ZZ /\ Z C_ B /\ F : B --> RR* ) /\
      B e. V /\ A e. RR* ) ->
      ( ( limsup ` F ) < A <-> E. j e. Z ( G ` j ) < A ) ) $=
      ( wcel cxr w3a cfv cle wbr wn wrex syl2anc cz wss wf clsp cv clt limsuple
      wral notbid rexnal syl6bbr wb cvv simp13 simp2 fex limsupcl simp3 xrltnle
      syl wa limsupgf ffvelrni adantl adantr rexbidva 3bitr4d ) GUALZIBUBZBMEUC
      ZNZBHLZAMLZNZAEUDOZPQZRZACUEZFOZPQZRZCISZVOAUFQZVSAUFQZCISVNVQVTCIUHZRWBV
      NVPWEABCDEFGHIJKUGUIVTCIUJUKVNVOMLZVMWCVQULVNEUMLZWFVNVJVLWGVHVIVJVLVMUNV
      KVLVMUOBMHEUPTEUMUQUTVKVLVMURZVOAUSTVNWDWACIVNVRILZVAVSMLZVMWDWAULWIWJVNI
      MVRFDEFIJVBVCVDVNVMWIWHVEVSAUSTVFVG $.
      $( [7-Sep-2014] $)

    $( If a sequence of real numbers has upper bounded limit supremum, then all
       the partial suprema are real.  (Contributed by Mario Carneiro,
       7-Sep-2014.) $)
    limsupgre $p |- ( ( F : Z --> RR /\ ( limsup ` F ) < +oo /\ N e. Z ) ->
      ( G ` N ) e. RR ) $=
      ( vx cr cpnf clt wbr wcel cxr wss syl cle c0 syl2anc vn clsp cfv w3a cima
      wf cuz csup wceq limsupgval 3ad2ant3 cv wrex simp2 cz wb syl6eleq eluzel2
      cin simp3 ssid a1i simp1 ressxr fss sylancl fvex eqeltri limsuplt mp3an23
      cvv pnfxr syl3anc mpbid wa c1 cmin co cfz cif wral cun simprl crn imassrn
      frn 3ad2ant1 syl6ss adantr syl5ss df-ss sylib supeq1d simprr eqbrtrrd wne
      eqtrd cdm fdm eleqtrrd uzssz eqsstri sseldi uzid inelcm imadisj necon3bii
      sylibr supxrre1 mpbird wor cfn xrltso cres fzfi wfun ad2antrr ffun fzssuz
      wfo uzss syl6eq sseqtr4d fofi sylancr zltlem1 peano2zm eluz bitr4d biimpa
      fores eluzfz1 fisup2g syl13anc supxrleub wi zre ltpnf xrlelttr mp2and imp
      sseldd max2 ifcl max1 ralun sylibrd uzsplit imaeq2d imaundi raleqdv inss2
      supxrcl ax-mp eqsstrd eluzelz eqnetrd biimpar limsupgord breqtrrd simplrr
      ltle 3syl limsupgf ffvelrni pm2.61ltlei expr rexlimdva mpd eqeltrd ) FJBU
      FZBUBUCKLMZEFNZUDZECUCZBEUGUCZUEZOUSZOLUHZJUVMUVKUVOUVSUIUVLABCEFGUJUKUVN
      UVSJNZUVSKLMZUVNUAULZCUCZKLMZUAFUMZUWAUVNUVLUWEUVKUVLUVMUNUVNDUONZFFPZFOB
      UFZUVLUWEUPZUVNEDUGUCZNZUWFUVNEFUWJUVKUVLUVMUTZHUQZDEURQUWGUVNFVAVBUVNUVK
      JOPUWHUVKUVLUVMVCZVDFJOBVEVFUWFUWGUWHUDFVKNKONZUWIFUWJVKHDUGVGVHVLKFUAABC
      DVKFGHVIVJVMVNUVNUWDUWAUAFUVNUWBFNZUWDUWAUVNUWPUWDVOZVOZUWAEUWBUWREUWBLMZ
      VOZUVTUWAUWTUVTUWAUWTUVSBUWBUGUCZUEZOLUHZBEUWBVPVQVRZVSVRZUEZOLUHZRMZUXGU
      XCVTZRMZUXIKLMZUWAUWTUXJIULUXIRMZIUVRWAZUWTUXMUXLIUXFUXBWBZWAZUWTUXLIUXFW
      AZUXLIUXBWAZUXOUWTUXGUXIRMZUXPUWTUXCJNZUXGJNZUXRUWRUXSUWSUWRUXSUXCKLMZUWR
      UWCUXCKLUWRUWCUXBOUSZOLUHZUXCUWRUWPUWCUYCUIZUVNUWPUWDWCZABCUWBFGUJZQUWROU
      YBUXBLUWRUXBOPZUYBUXBUIUWRUXBBWDZOBUXAWEZUVNUYHOPUWQUVNUYHJOUVKUVLUYHJPZU
      VMFJBWFWGZVDWHWIZWJZUXBOWKWLWMWQUVNUWPUWDWNWOUWRUXBJPUXBSWPZUXSUYAUPUWRUX
      BUYHJUYIUVNUYJUWQUYKWIZWJUWRBWRZUXAUSZSWPZUYNUWRUWBUYPNUWBUXANZUYRUWRUWBF
      UYPUYEUVNUYPFUIZUWQUVNUVKUYTUWNFJBWSQZWIZWTUWRUWBUONZUYSUWRFUOUWBFUWJUOHD
      XAXBZUYEXCZUWBXDQUWBUYPUXAXETUXBSUYQSBUXAXFXGXHUXBXITXJWIZUWTUXFJUXGUWTUX
      FUYHJBUXEWEZUWRUYJUWSUYOWIWJUWTOLXKZUXFXLNZUXFSWPZUXFOPZUXGUXFNVUHUWTXMVB
      UWTUXEXLNUXEUXFBUXEXNZXTZVUIEUXDXOUWTBXPZUXEUYPPVUMUWTUVKVUNUVNUVKUWQUWSU
      WNXQFJBXRQUWTUXEUVPUYPEUXDXSUWTUVPUWJUYPUWTUWKUVPUWJPUVNUWKUWQUWSUWMXQDEY
      AQUWTUYPFUWJUWRUYTUWSVUBWIHYBYCWJUXEBYKTUXEUXFVULYDYEUWTUYPUXEUSZSWPZVUJU
      WTEUYPNZEUXENZVUPUVNVUQUWQUWSUVNEFUYPUWLVUAWTZXQUWTUXDUVPNZVURUWRUWSVUTUW
      RUWSEUXDRMZVUTUWREUONZVUCUWSVVAUPUWRFUOEVUDUVNUVMUWQUWLWIXCZVUEEUWBYFTUWR
      VVBUXDUONZVUTVVAUPVVCUWRVUCVVDVUEUWBYGQEUXDYHTYIYJEUXDYLQEUYPUXEXETUXFSVU
      OSBUXEXFXGXHUWRVUKUWSUWRUXFUYHOVUGUYLWJWIZOUXFLYMYNUUBZUXCUXGUUCTUWTVUKUX
      IONZUXRUXPUPVVEUWTJOUXIVDUWTUXTUXSUXIJNZVVFVUFUXHUXGUXCJUUDTZXCZIUXFUXIYO
      TVNUWTUXCUXIRMZUXQUWTUXSUXTVVKVUFVVFUXCUXGUUETUWTUYGVVGVVKUXQUPUWRUYGUWSU
      YMWIVVJIUXBUXIYOTVNUXLIUXFUXBUUFTUWTUXLIUVRUXNUWTUVRUVQUXNUVNUVRUVQUIZUWQ
      UWSUVNUVQOPVVLUVNUVQJOUVNUVQUYHJBUVPWEUYKWJZVDWHUVQOWKWLZXQUWTUWBUVPNZUVQ
      UXNUIUWRUWSVVOUWRUWSEUWBRMZVVOUWREJNZUWBJNZUWSVVPYPUWRVVBVVQVVCEYQQZUWRVU
      CVVRVUEUWBYQQZEUWBUVBTUWRVVBVUCVVOVVPUPVVCVUEEUWBYHTUUGUUAVVOUVQBUXEUXAWB
      ZUEUXNVVOUVPVWABEUWBUUHUUIBUXEUXAUUJYBQWQUUKXJUWTUVROPZVVGUXJUXMUPUVQOUUL
      ZVVJIUVRUXIYOYEXJUWTVVHUXKVVIUXIYRQUWTUVSONZVVGUWOUXJUXKVOUWAYPVWDUWTVWBV
      WDVWCUVRUUMUUNZVBVVJUWOUWTVLVBUVSUXIKYSVMYTUWTUVRJPZUVRSWPZUVTUWAUPZUVNVW
      FUWQUWSUVNUVRUVQJVVNVVMUUOZXQUVNVWGUWQUWSUVNUVRUVQSVVNUVNUYPUVPUSZSWPZUVQ
      SWPUVNVUQEUVPNZVWKVUSUVNUWKVVBVWLUWMDEUUPEXDUVCEUYPUVPXETUVQSVWJSBUVPXFXG
      XHUUQZXQUVRXIZTXJUVSYRQUWRUWBERMZVOZUVSUWCRMZUWDUWAVWPUVSUYCUWCRVWPEUXANZ
      UVSUYCRMUWRVWRVWOUWRVUCVVBVWRVWOUPVUEVVCUWBEYHTUURBUWBEUUSQVWPUWPUYDUWRUW
      PVWOUYEWIZUYFQUUTUVNUWPUWDVWOUVAVWPVWDUWCONZUWOVWQUWDVOUWAYPVWDVWPVWEVBVW
      PUWPVWTVWSFOUWBCABCFGUVDUVEQUWOVWPVLVBUVSUWCKYSVMYTVVSVVTUVFUVGUVHUVIUVNV
      WFVWGVWHVWIVWMVWNTXJUVJ $.
      $( [7-Sep-2014] $)
  $}

  ${
    $d j k A $.  $d i j k n F $.  $d j k n M $.  $d j k ph $.  $d i j k n Z $.
    limsupbnd.1 $e |- Z = ( ZZ>= ` M ) $.
    limsupbnd.2 $e |- ( ph -> F : Z --> RR ) $.
    limsupbnd.3 $e |- ( ph -> A e. RR ) $.
    ${
      limsupbnd1.4 $e |- ( ph ->
        E. k e. Z A. j e. ( ZZ>= ` k ) ( F ` j ) <_ A ) $.
      $( If a sequence is eventually at most ` A ` , then the limsup is also at
         most ` A ` .  (The converse is only true if the less or equal is
         replaced by strictly less than; consider the sequence ` 1 / n ` which
         is never less or equal to zero even though the limsup is.)
         (Contributed by Mario Carneiro, 7-Sep-2014.) $)
      limsupbnd1 $p |- ( ph -> ( limsup ` F ) <_ A ) $=
        ( vn cfv cle wbr cuz wcel cxr cvv cr cv wral wrex clsp wa cima cin csup
        clt cmpt wf fvex eqeltri fex sylancl limsupcl syl xrleid cz wss eluzel2
        wb eleq2s a1d rexlimiv ssid a1i ressxr eqid limsuple syl311anc r19.21bi
        fss mpbid adantrr simprr simprl adantr sseldi limsupgle syl31anc mpbird
        wi limsupgf ffvelrni ad2antrl xrletr syl3anc mp2and expr rexlimdva mpd
        ) ACUAEMBNOCDUAZPMUBZDGUCZEUDMZBNOZKAWNWQDGAWMGQZWNWQAWRWNUEZUEZWPWMLGE
        LUAPMUFRUGRUIUHUJZMZNOZXBBNOZWQAWRXCWNAXCDGAWPWPNOZXCDGUBZAWPRQZXEAESQZ
        XGAGTEUKZGSQZXHIGFPMZSHFPULUMZGTSEUNUOESUPUQZWPURUQAFUSQZGGUTZGREUKZXJX
        GXEXFVBAWOXNKWNXNDGWRXNWNXNWMXKGFWMVAHVCVDVEUQXOAGVFZVGAXITRUTXPIVHGTRE
        VMUOZXJAXLVGXMWPGDLEXAFSGXAVIZHVJVKVNVLVOWTXDWNAWRWNVPWTWRXOXPBRQZXDWNV
        BAWRWNVQXOWTXQVGAXPWSXRVRAXTWSATRBVHJVSVRZBGCLEXAFWMGXSHVTWAWBWTXGXBRQZ
        XTXCXDUEWQWCAXGWSXMVRWRYBAWNGRWMXALEXAGXSWDWEWFYAWPXBBWGWHWIWJWKWL $.
        $( [7-Sep-2014] $)
    $}

    limsupbnd2.4 $e |- ( ph ->
      E. k e. Z A. j e. ( ZZ>= ` k ) A <_ ( F ` j ) ) $.
    $( If a sequence is eventually greater than ` A ` , then the limsup is also
       greater than ` A ` .  (Contributed by Mario Carneiro, 7-Sep-2014.) $)
    limsupbnd2 $p |- ( ph -> A <_ ( limsup ` F ) ) $=
      ( vn cfv cle wbr cuz wcel wa cxr cr wral wrex clsp cima cin clt csup cmpt
      vi cv cz uzssz eqsstri wss syl6eleq uzss syl syl6sseqr sselda sseldi uzid
      simpr eqid limsupgf ffvelrni xrleid wf wb adantr ressxr sylancl limsupgle
      fss syl31anc mpbid wceq fveq2 breq1d rcla4v sylc ad2antrr ffvelrn syl2anc
      wi adantl xrletr syl3anc mpan2d ralimdva impr simprl adantrr fvex eqeltri
      cvv a1i limsuple syl311anc mpbird expr rexlimdva mpd ) ABCUJZEMZNOZCDUJZP
      MZUAZDGUBBEUCMNOZKAXHXIDGAXFGQZXHXIAXJXHRZRZXIBXCLXGELUJPMUDSUESUFUGUHZMZ
      NOZCXGUAZAXJXHXPAXJRZXEXOCXGXQXCXGQZRZXEXDXNNOZXOXSXCXCPMZQZUIUJZEMZXNNOZ
      UIYAUAZXTXSXCUKQYBXSGUKXCGFPMZUKHFULUMZXQXGGXCXQXGYGGXQXFYGQXGYGUNXQXFGYG
      AXJVBHUOFXFUPUQHURZUSZUTXCVAUQXSXNXNNOZYFXSXNSQZYKXSXRYLXQXRVBZXGSXCXMLEX
      MXGXMVCZVDVEZUQZXNVFUQXSXRXGGUNZGSEVGZYLYKYFVHYMXQYQXRYIVIXQYRXRAYRXJAGTE
      VGZTSUNYRIVJGTSEVMVKZVIVIYPXNGUILEXMXFXCXGYNXGVCZVLVNVOYEXTUIXCYAYCXCVPYD
      XDXNNYCXCEVQVRVSVTXSBSQZXDSQYLXEXTRXOWDAUUBXJXRATSBVJJUTZWAXSTSXDVJXSYSXC
      GQXDTQAYSXJXRIWAYJGTXCEWBWCUTXRYLXQYOWEBXDXNWFWGWHWIWJXLXFUKQYQYRGWOQZUUB
      XIXPVHXLGUKXFYHAXJXHWKUTAXJYQXHYIWLAYRXKYTVIUUDXLGYGWOHFPWMWNWPAUUBXKUUCV
      IBGCLEXMXFWOXGYNUUAWQWRWSWTXAXB $.
      $( [7-Sep-2014] $)
  $}


