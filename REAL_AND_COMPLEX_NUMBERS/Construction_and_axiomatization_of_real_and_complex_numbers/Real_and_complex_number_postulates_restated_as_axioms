$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Real and complex number postulates restated as axioms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The real numbers are a subset of the complex numbers.  Axiom 1 of 22 for
     real and complex numbers, justified by theorem ~ axresscn . $)
  ax-resscn $a |- RR C_ CC $.

  $( 1 is a complex number.  Axiom 2 of 22 for real and complex numbers,
     justified by theorem ~ ax1cn . $)
  ax-1cn $a |- 1 e. CC $.

  $( ` _i ` is a complex number.  Axiom 3 of 22 for real and complex numbers,
     justified by theorem ~ axicn . $)
  ax-icn $a |- _i e. CC $.

  $( Closure law for addition of complex numbers.  Axiom 4 of 22 for real and
     complex numbers, justified by theorem ~ axaddcl . $)
  ax-addcl $a |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) e. CC ) $.

  $( Closure law for addition in the real subfield of complex numbers.  Axiom 6
     of 23 for real and complex numbers, justified by theorem ~ axaddrcl . $)
  ax-addrcl $a |- ( ( A e. RR /\ B e. RR ) -> ( A + B ) e. RR ) $.

  $( Closure law for multiplication of complex numbers.  Axiom 6 of 22 for real
     and complex numbers, justified by theorem ~ axmulcl . $)
  ax-mulcl $a |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) e. CC ) $.

  $( Closure law for multiplication in the real subfield of complex numbers.
     Axiom 7 of 22 for real and complex numbers, justified by theorem
     ~ axmulrcl . $)
  ax-mulrcl $a |- ( ( A e. RR /\ B e. RR ) -> ( A x. B ) e. RR ) $.

  $( Multiplication of complex numbers is commutative.  Axiom 8 of 22 for real
     and complex numbers, justified by theorem ~ axmulcom . $)
  ax-mulcom $a |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) = ( B x. A ) ) $.

  $( Addition of complex numbers is associative.  Axiom 9 of 22 for real and
     complex numbers, justified by theorem ~ axaddass . $)
  ax-addass $a |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A + B ) + C ) = ( A + ( B + C ) ) ) $.

  $( Multiplication of complex numbers is associative.  Axiom 10 of 22 for real
     and complex numbers, justified by theorem ~ axmulass . $)
  ax-mulass $a |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) ) $.

  $( Distributive law for complex numbers.  Axiom 11 of 22 for real and complex
     numbers, justified by theorem ~ axdistr . $)
  ax-distr $a |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
              ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) ) $.

  $( i-squared equals -1 (expressed as i-squared plus 1 is 0).  Axiom 12 of 22
     for real and complex numbers, justified by theorem ~ axi2m1 . $)
  ax-i2m1 $a |- ( ( _i x. _i ) + 1 ) = 0 $.

  $( 1 and 0 are distinct.  Axiom 13 of 22 for real and complex numbers,
     justified by theorem ~ ax1ne0 . $)
  ax-1ne0 $a |- 1 =/= 0 $.

  $( ` 1 ` is an identity element for real multiplication.  Axiom 14 of 22 for
     real and complex numbers, justified by theorem ~ ax1rid .  Weakened from
     the the original axiom in the form of statement in ~ mulid1 , based on
     ideas by Eric Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
  ax-1rid $a |- ( A e. RR -> ( A x. 1 ) = A ) $.

  ${
    $d x A $.
    $( Existence of negative of real number.  Axiom 15 of 22 for real and
       complex numbers, justified by theorem ~ axrnegex . $)
    ax-rnegex $a |- ( A e. RR -> E. x e. RR ( A + x ) = 0 ) $.

    $( Existence of reciprocal of nonzero real number.  Axiom 16 of 22 for real
       and complex numbers, justified by theorem ~ axrrecex . $)
    ax-rrecex $a |- ( ( A e. RR /\ A =/= 0 ) -> E. x e. RR ( A x. x ) = 1 ) $.
  $}

  ${
    $d x y A $.
    $( A complex number can be expressed in terms of two reals.  Definition
       10-1.1(v) of [Gleason] p. 130.  Axiom 17 of 22 for real and complex
       numbers, justified by theorem ~ axcnre . $)
    ax-cnre $a |- ( A e. CC ->
                      E. x e. RR E. y e. RR A = ( x + ( _i x. y ) ) ) $.
  $}

  $( Ordering on reals satisfies strict trichotomy.  Axiom 18 of 22 for real
     and complex numbers, justified by theorem ~ axpre-lttri .  Note:  The more
     general version for extended reals is ~ axlttri . $)
  ax-pre-lttri $a |- ( ( A e. RR /\ B e. RR ) ->
                ( A <RR B <-> -. ( A = B \/ B <RR A ) ) ) $.

  $( Ordering on reals is transitive.  Axiom 19 of 22 for real and complex
     numbers, justified by theorem ~ axpre-lttrn .  Note:  The more general
     version for extended reals is ~ axlttrn . $)
  ax-pre-lttrn $a |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <RR B /\ B <RR C ) -> A <RR C ) ) $.

  $( Ordering property of addition on reals.  Axiom 20 of 22 for real and
     complex numbers, justified by theorem ~ axpre-ltadd .  Note:  The more
     general version for extended reals is ~ axltadd . $)
  ax-pre-ltadd $a |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <RR B -> ( C + A ) <RR ( C + B ) ) ) $.

  $( The product of two positive reals is positive.  Axiom 21 of 22 for real
     and complex numbers, justified by theorem ~ axpre-mulgt0 .  Note:  The
     more general version for extended reals is ~ axmulgt0 . $)
  ax-pre-mulgt0 $a |- ( ( A e. RR /\ B e. RR ) ->
                ( ( 0 <RR A /\ 0 <RR B ) -> 0 <RR ( A x. B ) ) ) $.

  ${
    $d x y z A $.
    $( A non-empty, bounded-above set of reals has a supremum.  Axiom 22 of 22
       for real and complex numbers, justified by theorem ~ axpre-sup .  Note:
       The more general version with ordering on extended reals is ~ axsup . $)
    ax-pre-sup $a |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR
                    A. y e. A y <RR x ) -> E. x e. RR ( A. y e. A -. x <RR y /\
                    A. y e. RR ( y <RR x -> E. z e. A y <RR z ) ) ) $.
  $}

  $( Addition is an operation on the complex numbers.  This deprecated axiom is
     provided for historical compatibility but is not a bona fide axiom for
     complex numbers (independent of set theory) since it cannot be interpreted
     as a first- or second-order statement (see
     ~ http://us.metamath.org/downloads/schmidt-cnaxioms.pdf ). .  It may be
     deleted in the future and should be avoided for new theorems.  Instead,
     the less specific ~ ax-addcl should be used.  Note that uses of
     ~ ax-addopr can be eliminated by using the defined operation
     ` ( x e. CC , y e. CC |-> ( x + y ) ) ` in place of ` + ` , from which
     this axiom (with the defined operation in place of ` + ` ) follows as a
     theorem.

     This axiom is justified by theorem ~ axaddopr . $)
  ax-addopr $a |- + : ( CC X. CC ) --> CC $.

  $( Multiplication is an operation on the complex numbers.  This deprecated
     axiom is provided for historical compatibility but is not a bona fide
     axiom for complex numbers (independent of set theory) since it cannot be
     interpreted as a first- or second-order statement (see
     ~ http://us.metamath.org/downloads/schmidt-cnaxioms.pdf ). .  It may be
     deleted in the future and should be avoided for new theorems.  Instead,
     the less specific ~ ax-mulcl should be used.  Note that uses of
     ~ ax-mulopr can be eliminated by using the defined operation
     ` ( x e. CC , y e. CC |-> ( x x. y ) ) ` in place of ` x. ` , from which
     this axiom (with the defined operation in place of ` x. ` ) follows as a
     theorem.

     This axiom is justified by theorem ~ axmulopr . $)
  ax-mulopr $a |- x. : ( CC X. CC ) --> CC $.


