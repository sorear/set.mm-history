$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The natural numbers of analysis start at one (unlike the ordinal natural
     numbers, i.e. the members of the set ` om ` , ~ df-om , which start at
     zero).  This is the convention used by most analysis books, and it is
     often convenient in proofs because we don't have to worry about division
     by zero.  See ~ nnind for the principle of mathematical induction.  See
     ~ dfnn2 for a slight variant.  See ~ df-n0 for the set of nonnegative
     integers ` NN0 ` starting at zero.  See ~ dfn2 for ` NN ` defined in terms
     of ` NN0 ` .

     This is a technical definition that helps us avoid the Axiom of Infinity
     in certain proofs.  For a more conventional and intuitive definition ("the
     smallest set of reals containing ` 1 ` as well as the successor of every
     member") see ~ dfnn3 . $)
  df-n $a |- NN = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 1 ) " om ) $.

  $( The set of natural numbers exists.  (Revised by Mario Carneiro,
     3-May-2014.) $)
  nnex $p |- NN e. _V $=
    ( vx cn cvv cv c1 caddc co cmpt crdg com cima df-n wfun wcel con0 wfn fnfun
    rdgfnon ax-mp omex funimaexg mp2an eqeltri ) BACADEFGHZEIZJKZCALUEMZJCNUFCN
    UEOPUGEUDROUEQSTUEJCUAUBUC $.
    $( [3-May-2014] $) $( [3-Oct-1999] $)

  ${
    $d n x y z A $.
    $( Peano's inductive postulate.  Theorem I.36 (principle of mathematical
       induction) of [Apostol] p. 34.  (Revised by Mario Carneiro,
       3-May-2014.) $)
    peano5nni $p |- ( ( 1 e. A /\ A. x e. A ( x + 1 ) e. A ) -> NN C_ A ) $=
      ( vn vy vz c1 wcel cv caddc co wral cvv com cfv c0 wceq fveq2 eleq1d con0
      ax-mp wa cn cmpt crdg cima df-n wss csuc ax-1cn elexi rdg0 simpl syl5eqel
      cc wi oveq1 rcla4cv ad2antlr wb nnon rdgsuc syl fvex cbvmptv fvmpt syl6eq
      ovex adantl sylibrd expcom finds2 com12 ralrimiv cdm rdgfnon fnfun omsson
      wfun wfn fndm sseqtr4i funimass4 mp2an sylibr syl5eqss ) FBGZAHZFIJZBGZAB
      KZUAZUBCLCHZFIJZUCZFUDZMUEZBCUFWKDHZWONZBGZDMKZWPBUGZWKWSDMWQMGWKWSWSOWON
      ZBGEHZWONZBGZXCUHZWONZBGZWKDEWQOPWRXBBWQOWOQRWQXCPWRXDBWQXCWOQRWQXFPWRXGB
      WQXFWOQRWKXBFBFWNFUNUIUJUKWFWJULUMWKXCMGZXEXHUOWKXIUAXEXDFIJZBGZXHWJXEXKU
      OWFXIWIXKAXDBWGXDPWHXJBWGXDFIUPZRUQURXIXHXKUSWKXIXGXJBXIXGXDWNNZXJXIXCSGX
      GXMPXCUTFXCWNVAVBXDLGXMXJPXCWOVCAXDWHXJLWNXLCALWMWHWLWGFIUPVDXDFIVGVETVFR
      VHVIVJVKVLVMWOVRZMWOVNZUGXAWTUSWOSVSZXNFWNVOZSWOVPTMSXOVQXPXOSPXQSWOVTTWA
      DMBWOWBWCWDWE $.
      $( [3-May-2014] $) $( [10-Jan-1997] $)
  $}

  $( The natural numbers are a subset of the reals. $)
  nnssre $p |- NN C_ RR $=
    ( vx c1 cr wcel cv caddc co wral cn wss 1re peano2re rgen peano5nni mp2an )
    BCDAEZBFGCDZACHICJKQACPLMACNO $.
    $( [16-Jun-2013] $) $( [10-Jan-1997] $)

  $( The natural numbers are a subset of the complex numbers. $)
  nnsscn $p |- NN C_ CC $=
    ( cn cr cc nnssre ax-resscn sstri ) ABCDEF $.
    $( [2-Aug-2004] $)

  $( A natural number is a real number. $)
  nnre $p |- ( A e. NN -> A e. RR ) $=
    ( cn cr nnssre sseli ) BCADE $.
    $( [18-Aug-1999] $)

  $( A natural number is a complex number. $)
  nncn $p |- ( A e. NN -> A e. CC ) $=
    ( cn cc nnsscn sseli ) BCADE $.
    $( [18-Aug-1999] $)

  ${
    nnre.1 $e |- A e. NN $.
    $( A natural number is a real number. $)
    nnrei $p |- A e. RR $=
      ( cn wcel cr nnre ax-mp ) ACDAEDBAFG $.
      $( [18-Aug-1999] $)

    $( A natural number is a complex number. $)
    nncni $p |- A e. CC $=
      ( nnrei recni ) AABCD $.
      $( [18-Aug-1999] $)
  $}

  ${
    $d x y z A $.
    $( Peano postulate: 1 is a natural number.  (Revised by Mario Carneiro,
       3-May-2014.) $)
    1nn $p |- 1 e. NN $=
      ( vx c1 cvv cv caddc co cmpt crdg com cima cn c0 cfv cc ax-1cn elexi rdg0
      wcel con0 ax-mp peano1 wfun cdm wss wi wfn rdgfnon fnfun omsson wceq fndm
      sseqtr4i funfvima2 mp2an eqeltrri df-n eleqtrri ) BACADBEFGZBHZIJZKLUSMZB
      UTBURBNOPQLIRZVAUTRZUAUSUBZIUSUCZUDVBVCUEUSSUFZVDBURUGZSUSUHTISVEUIVFVESU
      JVGSUSUKTULILUSUMUNTUOAUPUQ $.
      $( [3-May-2014] $) $( [11-Jan-1997] $)

    $( Peano postulate: a successor of a natural number is a natural number. $)
    peano2nn $p |- ( A e. NN -> ( A + 1 ) e. NN ) $=
      ( vx vy vz c1 caddc co cn wcel cvv cv com cfv wceq wss omsson mp2an oveq1
      con0 ax-mp cmpt crdg cima wrex wfn rdgfnon fvelimab csuc nnon rdgsuc fvex
      wb syl cbvmptv ovex fvmpt syl6eq peano2 wfun cdm fnfun sseqtr4i funfvima2
      wi fndm df-n syl6eleqr eqeltrrd eleq1d syl5ibcom rexlimiv sylbi eleq2s )
      AEFGZHIZABJBKZEFGZUAZEUBZLUCZHAVTIZCKZVSMZANZCLUDZVOVSSUEZLSOWAWEULEVRUFZ
      PCSLAVSUGQWDVOCLWBLIZWCEFGZHIWDVOWHWBUHZVSMZWIHWHWKWCVRMZWIWHWBSIWKWLNWBU
      IEWBVRUJUMWCJIWLWINWBVSUKDWCDKZEFGZWIJVRWMWCEFRBDJVQWNVPWMEFRUNWCEFUOUPTU
      QWHWKVTHWHWJLIZWKVTIZWBURVSUSZLVSUTZOWOWPVDWFWQWGSVSVATLSWRPWFWRSNWGSVSVE
      TVBLWJVSVCQUMBVFZVGVHWDWIVNHWCAEFRVIVJVKVLWSVM $.
      $( [3-May-2014] $) $( [11-Jan-1997] $)

    $( Alternate definition of the set of natural numbers.  This was our
       original definition, before the current ~ df-n replaced it.  This
       definition requires the axiom of infinity to ensure it has the
       properties we expect.  (Contributed by Jeff Hankins, 12-Sep-2013.)
       (Revised by Mario Carneiro, 3-May-2014.) $)
    dfnn2 $p |- NN = |^| { x | ( 1 e. x /\ A. y e. x ( y + 1 ) e. x ) } $=
      ( vz cn c1 cv wcel caddc co wral wa cab cint wss wi elintab wal wceq rgen
      eleq2 ax-1cn elexi simpl mpgbir oveq1 eleq1d rcla4cv adantl a2i alimi vex
      cc ovex 3imtr4i peano5nni mp2an nnex raleqbi1dv anbi12d elab 1nn peano2nn
      mpbir2an intss1 ax-mp eqssi ) DEAFZGZBFZEHIZVGGZBVGJZKZALZMZEVOGZCFZEHIZV
      OGZCVOJDVONVPVMVHOAVMAEEULUAUBPVHVLUCUDVSCVOVMVQVGGZOZAQVMVRVGGZOZAQVQVOG
      VSWAWCAVMVTWBVLVTWBOVHVKWBBVQVGVIVQRVJVRVGVIVQEHUEUFUGUHUIUJVMAVQCUKPVMAV
      RVQEHUMPUNSCVOUOUPDVNGZVODNWDEDGZVJDGZBDJZVMWEWGKADUQVGDRVHWEVLWGVGDETVKW
      FBVGDVGDVJTURUSUTVAWFBDVIVBSVCDVNVDVEVF $.
      $( [3-May-2014] $)

    $( Alternate definition of the set of natural numbers.  Definition of
       positive integers in [Apostol] p. 22. $)
    dfnn3 $p |- NN =
               |^| { x | ( x C_ RR /\ 1 e. x /\ A. y e. x ( y + 1 ) e. x ) } $=
      ( vz cv cr wss c1 wcel caddc wral wa cab cint cn eleq2 raleqbi1dv anbi12d
      wceq dfnn2 pm3.2i co w3a wb eqeq2i sylbir nnssre eqsstr3i peano2nn intabs
      1nn rgen 3anass abbii inteqi 3eqtr4ri ) ADZEFZGUPHZBDZGIUAZUPHZBUPJZKZKZA
      LZMVCALMUQURVBUBZALZMNVCGCDZHZUTVHHZBVHJZKZGNHZUTNHZBNJZKZACEUPVHRURVIVBV
      KUPVHGOVAVJBUPVHUPVHUTOPQUPVLCLMZRUPNRZVCVPUCNVQUPCBSZUDVRURVMVBVOUPNGOVA
      VNBUPNUPNUTOPQUEVQEFVPVQNEVSUFUGVMVOUJVNBNUSUHUKTTUIVGVEVFVDAUQURVBULUMUN
      ABSUO $.
      $( [3-Jul-2005] $)
  $}

