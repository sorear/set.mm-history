$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Principle of mathematical induction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    nnind.1 $e |- ( x = 1 -> ( ph <-> ps ) ) $.
    nnind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    nnind.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    nnind.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    nnind.5 $e |- ps $.
    $( Induction hypothesis. $)
    nnind.6 $e |- ( y e. NN -> ( ch -> th ) ) $.
    $( Principle of Mathematical Induction (inference schema).  The first four
       hypotheses give us the substitution instances we need; the last two are
       the basis and the induction hypothesis.  See ~ nnaddcl for an example of
       its use.  See ~ nn0ind for induction on nonnegative integers and
       ~ uzind , ~ uzind4 for induction on an arbitrary set of upper integers.
       See ~ indstr for strong induction. $)
    nnind $p |- ( A e. NN -> ta ) $=
      ( cn wcel wa c1 elrab sseli crab cv caddc co wral wss 1nn mpbir2an ssrab2
      peano2nn a1d anim12d 3imtr4g mpcom rgen peano5nni mp2an sylib simprd ) HO
      PZUTEUTHAFOUAZPUTEQOVAHRVAPZGUBZRUCUDZVAPZGVAUEOVAUFVBROPBUGMABFROISUHVEG
      VAVCOPZVCVAPZVEVAOVCAFOUITVFVFCQVDOPZDQVGVEVFVFVHCDVFVHVFVCUJUKNULACFVCOJ
      SADFVDOKSUMUNUOGVAUPUQTAEFHOLSURUS $.
      $( [16-Jun-2013] $) $( [10-Jan-1997] $)
  $}

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Induction hypothesis. $)
    nnindALT.6 $e |- ( y e. NN -> ( ch -> th ) ) $.
    $( Basis. $)
    nnindALT.5 $e |- ps $.
    $( Substitutions. $)
    nnindALT.1 $e |- ( x = 1 -> ( ph <-> ps ) ) $.
    nnindALT.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    nnindALT.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    nnindALT.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Principle of Mathematical Induction (inference schema).  The last four
       hypotheses give us the substitution instances we need; the first two are
       the induction hypothesis and the basis.  (This ALT version of ~ nnind is
       easier to use with the Proof Assistant since 'assign last' will be
       applied to the substitution instances first.  We may switch to it as the
       official version.) $)
    nnindALT $p |- ( A e. NN -> ta ) $=
      ( nnind ) ABCDEFGHKLMNJIO $.
      $( [19-Aug-2007] $) $( [7-Dec-2005] $)
  $}

  ${
    $d x y A $.
    $( Every natural number is one or a successor.  (Contributed by Mario
       Carneiro, 16-May-2014.) $)
    nn1m1nn $p |- ( A e. NN -> ( A = 1 \/ ( A - 1 ) e. NN ) ) $=
      ( vx vy cv c1 wceq cmin co cn wcel wo cc caddc orc ax-1cn a1i eqeq1 oveq1
      eleq1d orbi12d 2thd nncn pncan sylancl id eqeltrd olcd a1d nnind ) BDZEFZ
      UJEGHZIJZKZELJZCDZEFZUPEGHZIJZKZUPEMHZEFZVAEGHZIJZKZAEFZAEGHZIJZKBCAUKUNU
      OUKUMNUOUKOPUAUJUPFZUKUQUMUSUJUPEQVIULURIUJUPEGRSTUJVAFZUKVBUMVDUJVAEQVJU
      LVCIUJVAEGRSTUJAFZUKVFUMVHUJAEQVKULVGIUJAEGRSTOUPIJZVEUTVLVDVBVLVCUPIVLUP
      LJUOVCUPFUPUBOUPEUCUDVLUEUFUGUHUI $.
      $( [16-May-2014] $)
  $}

  ${
    $d x y z $.  $d x y z A $.  $d x ps $.  $d x ch $.  $d x z th $.
    $d y z ph $.
    $( Substitutions. $)
    nn1suc.1 $e |- ( x = 1 -> ( ph <-> ps ) ) $.
    nn1suc.3 $e |- ( x = ( y + 1 ) -> ( ph <-> ch ) ) $.
    nn1suc.4 $e |- ( x = A -> ( ph <-> th ) ) $.
    $( Hypothesis showing it holds for 1. $)
    nn1suc.5 $e |- ps $.
    $( Hypothesis showing it holds for a successor. $)
    nn1suc.6 $e |- ( y e. NN -> ch ) $.
    $( If a statement holds for 1 and also holds for a successor, it holds for
       all natural numbers.  The first three hypotheses give us the
       substitution instances we need; the last two show that it holds for 1
       and for a successor.  (Revised by Mario Carneiro, 16-May-2014.) $)
    nn1suc $p |- ( A e. NN -> th ) $=
      ( cn wcel c1 wceq co wsbc wb caddc wo nn1m1nn 1nn elexi sbcie mpbir eleq1
      cmin wi mpbiri sbcieg syl dfsbcq bitr3d a1i cv ovex oveq1 syl5bbr vtoclga
      cc nncn ax-1cn npcan sylancl bitrd syl5ib jaod mpd ) GMNZGOPZGOUHQZMNZUAD
      GUBVJVKDVMVKDUIVJVKDAEORZVNBKABEOOMUCUDHUEUFVKAEGRZDVNVKVJVODSVKVJOMNUCGO
      MUGUJADEGMJUKZULAEGOUMUNUJUOVMAEVLOTQZRZVJDCVRFVLMCAEFUPZOTQZRZVSVLPZVRAC
      EVTVSOTUQIUEWBVTVQPWAVRSVSVLOTURAEVTVQUMULUSLUTVJVRVODVJVQGPZVRVOSVJGVANO
      VANWCGVBVCGOVDVEAEVQGUMULVPVFVGVHVI $.
      $( [11-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Closure of addition of natural numbers, proved by induction on the
       second addend. $)
    nnaddcl $p |- ( ( A e. NN /\ B e. NN ) -> ( A + B ) e. NN ) $=
      ( vx vy cn wcel caddc co cv wi c1 wceq oveq2 eleq1d imbi2d peano2nn wa cc
      nncn ax-1cn addass mp3an3 syl2an syl5ib expcom a2d nnind impcom ) BEFAEFZ
      ABGHZEFZUIACIZGHZEFZJUIAKGHZEFZJUIADIZGHZEFZJUIAUQKGHZGHZEFZJUIUKJCDBULKL
      ZUNUPUIVCUMUOEULKAGMNOULUQLZUNUSUIVDUMUREULUQAGMNOULUTLZUNVBUIVEUMVAEULUT
      AGMNOULBLZUNUKUIVFUMUJEULBAGMNOAPUQEFZUIUSVBUIVGUSVBJUSURKGHZEFUIVGQZVBUR
      PVIVHVAEUIARFZUQRFZVHVALZVGASUQSVJVKKRFVLTAUQKUAUBUCNUDUEUFUGUH $.
      $( [12-Jan-1997] $)

    $( Closure of multiplication of natural numbers. $)
    nnmulcl $p |- ( ( A e. NN /\ B e. NN ) -> ( A x. B ) e. NN ) $=
      ( vx vy cn wcel cmul co cv wi c1 caddc wceq oveq2 eleq1d imbi2d cc mulid1
      nncn wa biimprd nnaddcl ancoms ax-1cn mp3an3 oveq2d adantr syl2an syl5ibr
      mpcom adddi eqtrd exp4b pm2.43b a2d nnind impcom ) BEFAEFZABGHZEFZURACIZG
      HZEFZJURAKGHZEFZJURADIZGHZEFZJURAVFKLHZGHZEFZJURUTJCDBVAKMZVCVEURVLVBVDEV
      AKAGNOPVAVFMZVCVHURVMVBVGEVAVFAGNOPVAVIMZVCVKURVNVBVJEVAVIAGNOPVABMZVCUTU
      RVOVBUSEVABAGNOPAQFZURVEASZVPVEURVPVDAEARZOUAUJVFEFZURVHVKVSURVHVKJURVSUR
      VHVKURVHTVKURVSTZVGALHZEFZVHURWBVGAUBUCVTVJWAEURVPVFQFZVJWAMVSVQVFSVPWCTV
      JVGVDLHZWAVPWCKQFVJWDMUDAVFKUKUEVPWDWAMWCVPVDAVGLVRUFUGULUHOUIUMUNUOUPUQ
      $.
      $( [12-Jan-1997] $)
  $}

  ${
    nnmulcli.1 $e |- A e. NN $.
    nnmulcli.2 $e |- B e. NN $.
    $( Closure of multiplication of natural numbers.  (Contributed by Mario
       Carneiro, 18-Feb-2014.) $)
    nnmulcli $p |- ( A x. B ) e. NN $=
      ( cn wcel cmul co nnmulcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [18-Feb-2014] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( There exists a natural number greater than or equal to any two
       others. $)
    nn2ge $p |- ( ( A e. NN /\ B e. NN ) ->
                E. x e. NN ( A <_ x /\ B <_ x ) ) $=
      ( cn wcel wa cv cle wbr wrex cr nnre adantr leid sylan wceq breq2 anbi12d
      rcla4ev syldan adantl biantrud biimpa adantll anim1i adantlr lecasei ) BD
      EZCDEZFBAGZHIZCUJHIZFZADJZBCUHBKEZUIBLZMUICKEZUHCLZUAUIBCHIZUNUHUIUSUSCCH
      IZFZUNUIUQUSVAURUQUSVAUQUTUSCNUBUCOUMVAACDUJCPUKUSULUTUJCBHQUJCCHQRSTUDUH
      CBHIZUNUIUHVBBBHIZVBFZUNUHUOVBVDUPUOVCVBBNUEOUMVDABDUJBPUKVCULVBUJBBHQUJB
      CHQRSTUFUG $.
      $( [18-Aug-1999] $)
  $}

  ${
    $d x y A $.
    $( A natural number is one or greater. $)
    nnge1 $p |- ( A e. NN -> 1 <_ A ) $=
      ( vx vy c1 cv cle wbr caddc co breq2 1re wcel cr wi cc0 syl clt wn 0re wb
      leidi cn nnre cc wceq recn addid1 breq2d lt01 axltadd mp3an12 mpi readdcl
      mpan2 peano2re axlttrn mp3an3 syl2anc mpand con3d sylancr 3imtr4d sylbird
      wa lenlt nnind ) DBEZFGDDFGDCEZFGZDVHDHIZFGZDAFGBCAVGDDFJVGVHDFJVGVJDFJVG
      ADFJDKUAVHUBLVHMLZVIVKNVHUCVLVIDVHOHIZFGZVKVLVMVHDFVLVHUDLVMVHUEVHUFVHUGP
      UHVLVMDQGZRZVJDQGZRZVNVKVLVQVOVLVMVJQGZVQVOVLODQGZVSUIOMLZDMLZVLVTVSNSKOD
      VHUJUKULVLVMMLZVJMLZVSVQVDVONZVLWAWCSVHOUMUNZVHUOZWCWDWBWEKVMVJDUPUQURUSU
      TVLWBWCVNVPTKWFDVMVEVAVLWBWDVKVRTKWGDVJVEVAVBVCPVF $.
      $( [25-Aug-1999] $)
  $}

  $( A natural number is greater than one iff it is not equal to one. $)
  nngt1ne1 $p |- ( A e. NN -> ( 1 < A <-> A =/= 1 ) ) $=
    ( cn wcel cr c1 cle wbr clt wne wb nnre nnge1 1re leltne mp3an1 syl2anc ) A
    BCADCZEAFGZEAHGAEIJZAKALEDCQRSMEANOP $.
    $( [20-Jul-2007] $) $( [7-Oct-2004] $)

  $( A natural number is less than or equal to one iff it is equal to one. $)
  nnle1eq1 $p |- ( A e. NN -> ( A <_ 1 <-> A = 1 ) ) $=
    ( cn wcel c1 cle wbr wa wceq nnge1 biantrud cr wb 1re letri3 sylancl bitr4d
    nnre ) ABCZADEFZSDAEFZGZADHZRTSAIJRAKCDKCUBUALAQMADNOP $.
    $( [3-Apr-2005] $)

  $( A natural number is positive. $)
  nngt0 $p |- ( A e. NN -> 0 < A ) $=
    ( cn wcel cr c1 cle wbr cc0 clt nnre nnge1 lt01 wa wi 0re 1re mp3an12 mpani
    ltletr sylc ) ABCADCZEAFGZHAIGZAJAKUAHEIGZUBUCLHDCEDCUAUDUBMUCNOPHEASQRT $.
    $( [26-Sep-1999] $)

  $( A number less than one is not a natural number. $)
  lt1nnn $p |- ( A < 1 -> -. A e. NN ) $=
    ( cn wcel c1 clt wbr cle wn nnge1 cr wb 1re nnre lenlt sylancr mpbid con2i
    ) ABCZADEFZRDAGFZSHZAIRDJCAJCTUAKLAMDANOPQ $.
    $( [18-Jan-2004] $)

  $( Zero is not a natural number. $)
  0nnn $p |- -. 0 e. NN $=
    ( cc0 c1 clt wbr cn wcel wn lt01 lt1nnn ax-mp ) ABCDAEFGHAIJ $.
    $( [25-Aug-1999] $)

  $( A natural number is nonzero. $)
  nnne0 $p |- ( A e. NN -> A =/= 0 ) $=
    ( cn wcel cc0 wceq 0nnn eleq1 mtbiri necon2ai ) ABCZADADEJDBCFADBGHI $.
    $( [27-Sep-1999] $)

  ${
    nngt0.1 $e |- A e. NN $.
    $( A natural number is positive (inference version). $)
    nngt0i $p |- 0 < A $=
      ( cn wcel cc0 clt wbr nngt0 ax-mp ) ACDEAFGBAHI $.
      $( [17-Sep-1999] $)

    $( A natural number is nonzero (inference version). $)
    nnne0i $p |- A =/= 0 $=
      ( nnrei nngt0i gt0ne0ii ) AABCABDE $.
      $( [25-Aug-1999] $)
  $}

  $( The quotient of a real and a natural number is real. $)
  nndivre $p |- ( ( A e. RR /\ N e. NN ) -> ( A / N ) e. RR ) $=
    ( cn wcel cr cc0 wne wa cdiv co nnre nnne0 jca redivcl 3expb sylan2 ) BCDZA
    EDZBEDZBFGZHABIJEDZQSTBKBLMRSTUAABNOP $.
    $( [28-Nov-2008] $)

  $( The reciprocal of a natural number is real. $)
  nnrecre $p |- ( N e. NN -> ( 1 / N ) e. RR ) $=
    ( c1 cr wcel cn cdiv co 1re nndivre mpan ) BCDAEDBAFGCDHBAIJ $.
    $( [8-Feb-2008] $)

  $( The reciprocal of a natural number is positive. $)
  nnrecgt0 $p |- ( A e. NN -> 0 < ( 1 / A ) ) $=
    ( cn wcel c1 cle wbr cc0 cdiv co clt nnge1 lt01 cr wa wi 0re ltletr mp3an12
    nnre 1re recgt0 ex syld syl mpani mpd ) ABCZDAEFZGDAHIJFZAKUGGDJFZUHUILUGAM
    CZUJUHNZUIOASUKULGAJFZUIGMCDMCUKULUMOPTGDAQRUKUMUIAUAUBUCUDUEUF $.
    $( [25-Aug-1999] $)

  ${
    $d z A $.  $d x y z B $.
    $( Subtraction of natural numbers.  (Revised by Mario Carneiro,
       16-May-2014.) $)
    nnsub $p |- ( ( A e. NN /\ B e. NN ) -> ( A < B <-> ( B - A ) e. NN ) ) $=
      ( vz vx cn wcel clt wbr cmin co wi wral wceq breq2 eleq1d imbi12d ralbidv
      c1 oveq1 cr vy wa cv caddc con2i pm2.21d rgen breq1 oveq2 cbvralv nn1m1nn
      lt1nnn wo adantl nncn adantr ax-1cn pncan sylancl eqeltrd syl5ibrcom a1dd
      cc simpl rcla4v wb nnre 1re ltsubadd mp3an2 syl2anr subsub3 mp3an3 syl2an
      biimpd syl9r jaod mpd ralrimdva syl5bi nnind rcla4va sylan2 nngt0 syl5ibr
      cc0 posdif impbid ) AEFZBEFZUBZABGHZBAIJZEFZWJWICUCZBGHZBWOIJZEFZKZCELZWL
      WNKZWODUCZGHZXBWOIJZEFZKZCELWORGHZRWOIJZEFZKZCELWOUAUCZGHZXKWOIJZEFZKZCEL
      ZWOXKRUDJZGHZXQWOIJZEFZKZCELZWTDUABXBRMZXFXJCEYCXCXGXEXIXBRWOGNYCXDXHEXBR
      WOISOPQXBXKMZXFXOCEYDXCXLXEXNXBXKWOGNYDXDXMEXBXKWOISOPQXBXQMZXFYACEYEXCXR
      XEXTXBXQWOGNYEXDXSEXBXQWOISOPQXBBMZXFWSCEYFXCWPXEWRXBBWOGNYFXDWQEXBBWOISO
      PQXJCEWOEFZXGXIXGYGWOULUEUFUGXPXBXKGHZXKXBIJZEFZKZDELZXKEFZYBXOYKCDEWOXBM
      ZXLYHXNYJWOXBXKGUHYNXMYIEWOXBXKIUIOPUJYMYLYACEYMYGUBZWORMZWORIJZEFZUMZYLY
      AKZYGYSYMWOUKUNYOYPYTYRYOYPYAYLYOYPXTXRYOXTYPXQRIJZEFYOUUAXKEYOXKVCFZRVCF
      ZUUAXKMYMUUBYGXKUOZUPUQXKRURUSYMYGVDUTYPXSUUAEWORXQIUIOVAVBVBYRYLYQXKGHZX
      KYQIJZEFZKZYOYAYKUUHDYQEXBYQMZYHUUEYJUUGXBYQXKGUHUUIYIUUFEXBYQXKIUIOPVEYO
      UUHYAYOUUEXRUUGXTYGWOTFZXKTFZUUEXRVFZYMWOVGXKVGUUJRTFUUKUULVHWORXKVIVJVKY
      OUUFXSEYMUUBWOVCFZUUFXSMZYGUUDWOUOUUBUUMUUCUUNUQXKWORVLVMVNOPVOVPVQVRVSVT
      WAWSXACAEWOAMZWPWLWRWNWOABGUHUUOWQWMEWOABIUIOPWBWCWNWLWKWFWMGHZWMWDWIATFB
      TFWLUUPVFWJAVGBVGABWGVNWEWH $.
      $( [16-May-2014] $) $( [20-Aug-2001] $)

    nnsub.1 $e |- A e. NN $.
    nnsub.2 $e |- B e. NN $.
    $( Subtraction of natural numbers. $)
    nnsubi $p |- ( A < B <-> ( B - A ) e. NN ) $=
      ( cn wcel clt wbr cmin co wb nnsub mp2an ) AEFBEFABGHBAIJEFKCDABLM $.
      $( [19-Aug-2001] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Two ways to express " ` A ` divides ` B ` " for natural numbers.  (The
       proof was shortened by Mario Carneiro, 16-May-2014.) $)
    nndiv $p |- ( ( A e. NN /\ B e. NN ) ->
                ( E. x e. NN ( A x. x ) = B <-> ( B / A ) e. NN ) ) $=
      ( cdiv co cn wcel cv wceq wrex wa cmul risset eqcom cc0 wne nncn ad2antrr
      cc wb ad2antlr adantl nnne0 divmul syl112anc syl5bb rexbidva syl5rbb ) CB
      DEZFGAHZUIIZAFJBFGZCFGZKZBUJLECIZAFJAUIFMUNUKUOAFUKUIUJIZUNUJFGZKZUOUJUIN
      URCSGZUJSGZBSGZBOPZUPUOTUMUSULUQCQUAUQUTUNUJQUBULVAUMUQBQRULVBUMUQBUCRCUJ
      BUDUEUFUGUH $.
      $( [16-May-2014] $) $( [3-Feb-2004] $)
  $}

  $( Transitive property of divisibility: if ` A ` divides ` B ` and ` B `
     divides ` C ` , then ` A ` divides ` C ` .  Typically ` C ` would be an
     integer, although the theorem holds for complex ` C ` . $)
  nndivtr $p |- ( ( ( A e. NN /\ B e. NN /\ C e. CC ) /\
               ( ( B / A ) e. NN /\ ( C / B ) e. NN ) ) -> ( C / A ) e. NN ) $=
    ( cn wcel cc w3a cdiv co wa cmul nnmulcl c1 cc0 wne wceq 3ad2ant2 nnne0 jca
    nncn simp3 3ad2ant1 divmul24 divid syl2anc oveq1d divcl 3expb sylan2 ancoms
    syl22anc mulid2 syl 3adant2 3eqtrd eleq1d syl5ib imp ) ADEZBDEZCFEZGZBAHIZD
    ECBHIZDEJZCAHIZDEZVEVCVDKIZDEVBVGVCVDLVBVHVFDVBVHBBHIZVFKIZMVFKIZVFVBBFEZVA
    AFEZANOZJZVLBNOZJZVHVJPUTUSVLVABTZQUSUTVAUAUSUTVOVAUSVMVNATARSZUBUTUSVQVAUT
    VLVPVRBRZSQBCABUCUKUTUSVJVKPVAUTVIMVFKUTVLVPVIMPVRVTBUDUEUFQUSVAVKVFPZUTUSV
    AJVFFEZWAVAUSWBUSVAVOWBVSVAVMVNWBCAUGUHUIUJVFULUMUNUOUPUQUR $.
    $( [3-May-2005] $)

