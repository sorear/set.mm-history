$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Upper partititions of integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ZZ>= $. $( ` ZZ ` with greater-than-or-equal-to subscript. $)

  $( Extend class notation with the upper integer function.
     Read " ` ZZ>= `` M ` " as "the set of integers greater than or equal to
     ` M ` ." $)
  cuz $a class ZZ>= $.

  ${
    $d j k y N $.  $d k M $.
    $( Define a function whose value at ` j ` is the semi-infinite set of
       contiguous integers starting at ` j ` , which we will also call the
       upper integers starting at ` j ` .  Read " ` ZZ>= `` M ` " as "the set
       of integers greater than or equal to ` M ` ."  See ~ uzval for its
       value, ~ uzssz for its relationship to ` ZZ ` , ~ nnuz and ~ nn0uz for
       its relationships to ` NN ` and ` NN0 ` , and ~ eluz1 and ~ eluz2 for
       its membership relations. $)
    df-uz $a |- ZZ>= = ( j e. ZZ |-> { k e. ZZ | j <_ k } ) $.

    $( The value of the upper integers function.  (Revised by Mario Carneiro,
       3-Nov-2013.) $)
    uzval $p |- ( N e. ZZ -> ( ZZ>= ` N ) = { k e. ZZ | N <_ k } ) $=
      ( vj cv cle wbr cz crab cuz wceq breq1 rabbidv df-uz zex rabex fvmpt ) CB
      CDZADZEFZAGHBREFZAGHGIQBJSTAGQBREKLCAMTAGNOP $.
      $( [3-Nov-2013] $) $( [5-Sep-2005] $)

    $( The domain and range of the upper integers function.  (Contributed by
       Scott Fenton, 8-Aug-2013.)  (Revised by Mario Carneiro, 3-Nov-2013.) $)
    uzf $p |- ZZ>= : ZZ --> ~P ZZ $=
      ( vj vk cv cle wbr cz crab cpw wcel wral cuz wss ssrab2 elpw2 mpbir rgenw
      wf zex df-uz fmpt mpbi ) ACBCDEZBFGZFHZIZAFJFUDKQUEAFUEUCFLUBBFMUCFRNOPAF
      UDUCKABSTUA $.
      $( [3-Nov-2013] $)

    $( Membership in the set of upper integers starting at ` M ` . $)
    eluz1 $p |- ( M e. ZZ ->
           ( N e. ( ZZ>= ` M ) <-> ( N e. ZZ /\ M <_ N ) ) ) $=
      ( vk cz wcel cuz cfv cv cle wbr crab wa uzval eleq2d breq2 elrab syl6bb )
      ADEZBAFGZEBACHZIJZCDKZEBDEABIJZLRSUBBCAMNUAUCCBDTBAIOPQ $.
      $( [5-Sep-2005] $)

    $( Implication of membership in a set of upper integers.  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    eluzel2 $p |- ( N e. ( ZZ>= ` M ) -> M e. ZZ ) $=
      ( cuz cfv wcel cdm cz elfvdm cpw uzf fdmi syl6eleq ) BACDEACFGBACHGGICJKL
      $.
      $( [3-Nov-2013] $) $( [6-Sep-2005] $)

    $( Membership in a set of upper integers.  We use the fact that a
       function's value (under our function value definition) is empty outside
       of its domain to show ` M e. ZZ ` .  (Revised by Mario Carneiro,
       3-Nov-2013.) $)
    eluz2 $p |- ( N e. ( ZZ>= ` M ) <-> ( M e. ZZ /\ N e. ZZ /\ M <_ N ) ) $=
      ( cuz cfv wcel cz cle wbr w3a eluzel2 simp1 wa eluz1 bitrd 3anass syl6bbr
      ibar pm5.21nii ) BACDEZAFEZTBFEZABGHZIZABJTUAUBKTSTUAUBLZLZUCTSUDUEABMTUD
      QNTUAUBOPR $.
      $( [3-Nov-2013] $) $( [5-Sep-2005] $)
  $}

  ${
    eluz.1 $e |- M e. ZZ $.
    $( Membership in a set of upper integers. $)
    eluz1i $p |- ( N e. ( ZZ>= ` M ) <-> ( N e. ZZ /\ M <_ N ) ) $=
      ( cz wcel cuz cfv cle wbr wa wb eluz1 ax-mp ) ADEBAFGEBDEABHIJKCABLM $.
      $( [5-Sep-2005] $)
  $}

  $( A member of a set of upper integers is an integer. $)
  eluzelz $p |- ( N e. ( ZZ>= ` M ) -> N e. ZZ ) $=
    ( cuz cfv wcel cz cle wbr eluz2 simp2bi ) BACDEAFEBFEABGHABIJ $.
    $( [6-Sep-2005] $)

  $( A member of a set of upper integers is a real. $)
  eluzelre $p |- ( N e. ( ZZ>= ` M ) -> N e. RR ) $=
    ( cuz cfv wcel cz cr eluzelz zre syl ) BACDEBFEBGEABHBIJ $.
    $( [31-Aug-2013] $)

  $( Implication of membership in a set of upper integers. $)
  eluzle $p |- ( N e. ( ZZ>= ` M ) -> M <_ N ) $=
    ( cuz cfv wcel cz cle wbr eluz2 simp3bi ) BACDEAFEBFEABGHABIJ $.
    $( [6-Sep-2005] $)

  $( Membership in a set of upper integers. $)
  eluz $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
         ( N e. ( ZZ>= ` M ) <-> M <_ N ) ) $=
    ( cz wcel cuz cfv cle wb wa wi eluzelz pm4.71ri eluz1 syl5bbr pm5.32 sylibr
    wbr imp ) ACDZBCDZBAEFDZABGQZHZSTUAIZTUBIZHTUCJUDUASUEUATABKLABMNTUAUBOPR
    $.
    $( [2-Oct-2005] $)

  $( Membership of the least member in a set of upper integers. $)
  uzid $p |- ( M e. ZZ -> M e. ( ZZ>= ` M ) ) $=
    ( cz wcel cuz cfv cle wbr wa cr zre leid syl ancli eluz1 mpbird ) ABCZAADEC
    PAAFGZHPQPAICQAJAKLMAANO $.
    $( [2-Sep-2005] $)
  ${
    $d k M $.
    $( The upper integers are all nonempty.  (Contributed by Mario Carneiro,
       16-Jan-2014.) $)
    uzn0 $p |- ( M e. ran ZZ>= -> M =/= (/) ) $=
      ( vk cuz crn wcel cv cfv wceq cz wrex c0 wne cpw wfn uzf ffn fvelrnb mp2b
      wf wb uzid ne0i syl neeq1 syl5ibcom rexlimiv sylbi ) ACDEZBFZCGZAHZBIJZAK
      LZIIMZCSCINUHULTOIUNCPBIACQRUKUMBIUIIEZUJKLZUKUMUOUIUJEUPUIUAUJUIUBUCUJAK
      UDUEUFUG $.
      $( [16-Jan-2014] $)
  $}

  $( Transitive law for sets of upper integers. $)
  uztrn $p |- ( ( M e. ( ZZ>= ` K ) /\ K e. ( ZZ>= ` N ) ) ->
              M e. ( ZZ>= ` N ) ) $=
    ( cuz cfv wcel wa cz cle wbr eluzel2 adantl eluzelz adantr eluzle wi cr zre
    letr syl3an syl3anc mp2and eluz2 syl3anbrc ) BADEFZACDEZFZGZCHFZBHFZCBIJZBU
    FFUGUIUECAKLZUEUJUGABMNZUHCAIJZABIJZUKUGUNUECAOLUEUOUGABONUHUIAHFZUJUNUOGUK
    PZULUGUPUECAMLUMUICQFUPAQFUJBQFUQCRARBRCABSTUAUBCBUCUD $.
    $( [20-Sep-2005] $)

  ${
    uztrn2.1 $e |- Z = ( ZZ>= ` K ) $.
    $( Transitive law for sets of upper integers.  (Contributed by Mario
       Carneiro, 26-Dec-2013.) $)
    uztrn2 $p |- ( ( N e. Z /\ M e. ( ZZ>= ` N ) ) -> M e. Z ) $=
      ( wcel cuz cfv wa eleq2i uztrn ancoms sylanb syl6eleqr ) CDFZBCGHFZIBAGHZ
      DOCQFZPBQFZDQCEJPRSCBAKLMEN $.
      $( [26-Dec-2013] $)
  $}

  $( Contraposition law for upper integers. $)
  uzneg $p |- ( N e. ( ZZ>= ` M ) -> -u M e. ( ZZ>= ` -u N ) ) $=
    ( cuz cfv wcel cneg cle wbr eluzle cz wb eluzel2 eluzelz zre syl2an syl2anc
    cr leneg mpbid znegcl eluz mpbird ) BACDEZAFZBFZCDEZUEUDGHZUCABGHZUGABIUCAJ
    EZBJEZUHUGKZABLZABMZUIAQEBQEUKUJANBNABROPSUCUJUIUFUGKZUMULUJUEJEUDJEUNUIBTA
    TUEUDUAOPUB $.
    $( [28-Nov-2005] $)

  ${
    $d j k y M $.  $d k N $.
    $( A set of upper integers is a subset of all integers.  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    uzssz $p |- ( ZZ>= ` M ) C_ ZZ $=
      ( cuz cdm wcel cfv cz wss cpw uzf ffvelrni elpwi syl fdmi eleq2s wn ndmfv
      c0 0ss a1i eqsstrd pm2.61i ) ABCZDZABEZFGZUEAFUBAFDUDFHZDUEFUFABIJUDFKLFU
      FBIMNUCOZUDQFABPQFGUGFRSTUA $.
      $( [3-Nov-2013] $) $( [2-Sep-2005] $)

    $( Subset relationship for two sets of upper integers. $)
    uzss $p |- ( N e. ( ZZ>= ` M ) ->
               ( ZZ>= ` N ) C_ ( ZZ>= ` M ) ) $=
      ( vk cuz cfv wcel cv cz cle wbr wa eluzle adantr wi eluzel2 cr zre wb syl
      eluz1 eluzelz jca letr syl3an 3expa sylan mpand imdistanda 3imtr4d ssrdv
      ) BADEZFZCBDEZUKULCGZHFZBUNIJZKZUOAUNIJZKZUNUMFZUNUKFZULUOUPURULUOKABIJZU
      PURULVBUOABLMULAHFZBHFZKUOVBUPKURNZULVCVDABOZABUAZUBVCVDUOVEVCAPFVDBPFUOU
      NPFVEAQBQUNQABUNUCUDUEUFUGUHULVDUTUQRVGBUNTSULVCVAUSRVFAUNTSUIUJ $.
      $( [5-Sep-2005] $)
  $}

  $( Totality of the ordering relation on integers, stated in terms of upper
     integers.  (Contributed by Mario Carneiro, 25-Jun-2013.) $)
  uztric $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
      ( N e. ( ZZ>= ` M ) \/ M e. ( ZZ>= ` N ) ) ) $=
    ( cz wcel wa cuz cfv wo cle wbr cr zre letric syl2an eluz wb ancoms orbi12d
    mpbird ) ACDZBCDZEZBAFGDZABFGDZHABIJZBAIJZHZTAKDBKDUGUAALBLABMNUBUCUEUDUFAB
    OUATUDUFPBAOQRS $.
    $( [25-Jun-2013] $) $( [6-Jul-2005] $)

  $( The upper integers function is one-to-one. $)
  uz11 $p |- ( M e. ZZ ->
                ( ( ZZ>= ` M ) = ( ZZ>= ` N ) <-> M = N ) ) $=
    ( cz wcel cuz cfv wceq wa eleq2 eluzel2 syl6bi mpan9 cle wbr syl5ibr eluzle
    uzid syl6 cr zre syl5ib anim12d impl ancoms anassrs wb letri3 syl2an mpbird
    adantlr mpdan ex fveq2 impbid1 ) ACDZAEFZBEFZGZABGZUOURUSUOURHZBCDZUSUOAUPD
    ZURVAAQZURVBAUQDZVAUPUQAIZBAJKLUTVAHUSABMNZBAMNZHZUOURVAVHURVAHUOVHURVAUOVH
    URVAVFUOVGURVABUPDZVFVAVIURBUQDBQUPUQBIOABPRURUOVDVGUOVBURVDVCVEUABAPRUBUCU
    DUEUOVAUSVHUFZURUOASDBSDVJVAATBTABUGUHUJUIUKULABEUMUN $.
    $( [12-Dec-2005] $)

  $( Membership in the next set of upper integers. $)
  eluzp1m1 $p |- ( ( M e. ZZ /\
             N e. ( ZZ>= ` ( M + 1 ) ) ) -> ( N - 1 ) e. ( ZZ>= ` M ) ) $=
    ( cz wcel c1 caddc co cuz cfv cmin cle wbr wa peano2zm ad2antrl cr wb eluz1
    zre 1re leaddsub mp3an2 syl2an biimpa anasss jca ex peano2z syl 3imtr4d imp
    ) ACDZBAEFGZHIDZBEJGZAHIDZULBCDZUMBKLZMZUOCDZAUOKLZMZUNUPULUSVBULUSMUTVAUQU
    TULURBNOULUQURVAULUQMURVAULAPDZBPDZURVAQZUQASBSVCEPDVDVETAEBUAUBUCUDUEUFUGU
    LUMCDUNUSQAUHUMBRUIAUORUJUK $.
    $( [12-Sep-2005] $)

  $( Strict ordering implied by membership in the next set of upper
     integers. $)
  eluzp1l $p |- ( ( M e. ZZ /\
             N e. ( ZZ>= ` ( M + 1 ) ) ) -> M < N ) $=
    ( cz wcel c1 caddc cuz cfv clt wbr cle eluzle adantl eluzelz zltp1le sylan2
    co wa wb mpbird ) ACDZBAEFQZGHDZRABIJZUBBKJZUCUEUAUBBLMUCUABCDUDUESUBBNABOP
    T $.
    $( [12-Sep-2005] $)

  $( Membership in the next set of upper integers. $)
  eluzp1p1 $p |- ( N e. ( ZZ>= ` M ) ->
                       ( N + 1 ) e. ( ZZ>= ` ( M + 1 ) ) ) $=
    ( cz wcel cle wbr w3a c1 caddc co cuz cfv peano2z 3ad2ant1 3ad2ant2 zre 1re
    cr wb eluz2 leadd1 mp3an3 syl2an biimp3a 3jca 3imtr4i ) ACDZBCDZABEFZGZAHIJ
    ZCDZBHIJZCDZUKUMEFZGBAKLDUMUKKLDUJULUNUOUGUHULUIAMNUHUGUNUIBMOUGUHUIUOUGARD
    ZBRDZUIUOSZUHAPBPUPUQHRDURQABHUAUBUCUDUEABTUKUMTUF $.
    $( [5-Oct-2005] $)

  ${
    eluzaddi.1 $e |- M e. ZZ $.
    eluzaddi.2 $e |- K e. ZZ $.
    $( Membership in a later set of upper integers.  (Contributed by Paul
       Chapman, 22-Nov-2007.) $)
    eluzaddi $p |- ( N e. ( ZZ>= ` M )
          -> ( N + K ) e. ( ZZ>= ` ( M + K ) ) ) $=
      ( cuz cfv wcel caddc co cz cle wbr eluzelz zaddcl sylancl wa eluz1i zrei
      cr wb zre leadd1 mp3an13 syl biimpa sylbi mp2an sylanbrc ) CBFGHZCAIJZKHZ
      BAIJZUKLMZUKUMFGHUJCKHZAKHZULBCNECAOPUJUOBCLMZQUNBCDRUOUQUNUOCTHZUQUNUAZC
      UBBTHURATHUSBDSAESBCAUCUDUEUFUGUMUKBKHUPUMKHDEBAOUHRUI $.
      $( [22-Nov-2007] $)

    $( Membership in an earlier set of upper integers.  (Contributed by Paul
       Chapman, 22-Nov-2007.) $)
    eluzsubi $p |- ( N e. ( ZZ>= ` ( M + K ) )
         -> ( N - K ) e. ( ZZ>= ` M ) ) $=
      ( caddc co cuz cfv wcel cmin cz cle eluzelz zsubcl sylancl eluz1i cr zrei
      wbr wa zaddcl mp2an wb zre leaddsub mp3an12 syl biimpa sylbi sylanbrc ) C
      BAFGZHIJZCAKGZLJZBUNMTZUNBHIJUMCLJZALJZUOULCNECAOPUMUQULCMTZUAUPULCBLJURU
      LLJDEBAUBUCQUQUSUPUQCRJZUSUPUDZCUEBRJARJUTVABDSAESBACUFUGUHUIUJBUNDQUK $.
      $( [22-Nov-2007] $)
  $}

  $( Membership in a later set of upper integers.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  eluzadd $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. ZZ ) ->
                                      ( N + K ) e. ( ZZ>= ` ( M + K ) ) ) $=
    ( cuz cfv wcel cz caddc co eluzel2 wa wi cc0 cif eleq2d fveq2d oveq2 elimel
    wceq 0z fveq2 oveq1 imbi12d eleq12d imbi2d eluzaddi dedth2h com12 mpand imp
    ) CBDEZFZAGFZCAHIZBAHIZDEZFZULBGFZUMUQBCJURUMKULUQURUMULUQLCURBMNZDEZFZUNUS
    AHIZDEZFZLVACUMAMNZHIZUSVEHIZDEZFZLBAMMBUSSZULVAUQVDVJUKUTCBUSDUAOVJUPVCUNV
    JUOVBDBUSAHUBPOUCAVESZVDVIVAVKUNVFVCVHAVECHQVKVBVGDAVEUSHQPUDUEVEUSCBMGTRAM
    GTRUFUGUHUIUJ $.
    $( [2-Sep-2009] $)

  $( Membership in an earlier set of upper integers.  (Contributed by Jeff
     Madsen, 2-Sep-2009.) $)
  eluzsub $p |- ( ( M e. ZZ /\ K e. ZZ /\ N e. ( ZZ>= ` ( M + K ) ) )
                                          -> ( N - K ) e. ( ZZ>= ` M ) ) $=
    ( cz wcel caddc co cuz cfv cmin wi cc0 cif wceq fveq2d eleq2d imbi12d oveq2
    0z elimel oveq1 fveq2 eleq1d eluzsubi dedth2h 3impia ) BDEZADEZCBAFGZHIZEZC
    AJGZBHIZEZUGUHUKUNKCUGBLMZAFGZHIZEZULUOHIZEZKCUOUHALMZFGZHIZEZCVAJGZUSEZKBA
    LLBUONZUKURUNUTVGUJUQCVGUIUPHBUOAFUAOPVGUMUSULBUOHUBPQAVANZURVDUTVFVHUQVCCV
    HUPVBHAVAUOFROPVHULVEUSAVACJRUCQVAUOCBLDSTALDSTUDUEUF $.
    $( [2-Sep-2009] $)

  $( Choices for an element of an upper interval of integers.  (Contributed by
     Jeff Madsen, 2-Sep-2009.) $)
  uzm1 $p |- ( N e. ( ZZ>= ` M ) -> ( N = M \/ ( N - 1 ) e. ( ZZ>= ` M ) ) ) $=
    ( cuz cfv wcel wceq c1 cmin co wn cz cle wbr w3a eluzel2 a1d syl cr syl2anc
    wb eluzelz peano2zm clt wne df-ne eluzle zre eluzelre ltlen biimprd syl5bir
    wa mpand zltlem1 sylibd 3jcad eluz2 syl6ibr orrd ) BACDZEZBAFZBGHIZUTEZVAVB
    JZAKEZVCKEZAVCLMZNVDVAVEVFVGVHVAVFVEABOZPVAVGVEVABKEZVGABUAZBUBQPVAVEABUCMZ
    VHVEBAUDZVAVLBAUEVAABLMZVMVLABUFVAVLVNVMULZVAAREZBREVLVOTVAVFVPVIAUGQABUHAB
    UISUJUMUKVAVFVJVLVHTVIVKABUNSUOUPAVCUQURUS $.
    $( [2-Sep-2009] $)
  $( Intersection of two upper intervals of integers.  (Contributed by Mario
     Carneiro, 24-Dec-2013.) $)
  uzin $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
    ( ( ZZ>= ` M ) i^i ( ZZ>= ` N ) ) = ( ZZ>= ` if ( M <_ N , N , M ) ) ) $=
    ( cz wcel wa cuz cfv wo cle wbr wceq wss uzss sylib eluzle fveq2d eqtr4d cr
    syl zre cin cif uztric sseqin2 iftrue eluzel2 eluzelz letri3 syl2an syl2anc
    df-ss biantrurd bitr4d biimprcd eqeq1d sylibrd com12 iffalse pm2.61d1 jaoi
    wb ) ACDZBCDZEBAFGZDZABFGZDZHVDVFUAZABIJZBAUBZFGZKZABUCVEVLVGVEVHVFVKVEVFVD
    LVHVFKABMVFVDUDNVEVJBFVEVIVJBKABOVIBAUEZSPQVGVHVDVKVGVDVFLVHVDKBAMVDVFUKNVG
    VJAFVGVIVJAKZVIVGVNVIVGBAKZVNVGVOVIVGVOBAIJZVIEZVIVGVCVBVOVQVAZBAUFBAUGVCBR
    DARDVRVBBTATBAUHUIUJVGVPVIBAOULUMUNVIVJBAVMUOUPUQVIBAURUSPQUTS $.
    $( [24-Dec-2013] $)

  $( Choices for an element of an upper interval of integers.  (Contributed by
     Jeff Madsen, 2-Sep-2009.) $)
  uzp1 $p |- ( N e. ( ZZ>= ` M ) -> ( N = M \/ N e. ( ZZ>= ` ( M + 1 ) ) ) ) $=
    ( cuz cfv wcel wceq c1 cmin co wo caddc uzm1 eluzp1p1 cz eluzelz zcn ax-1cn
    cc npcan mpan2 3syl eleq1d syl5ib orim2d mpd ) BACDZEZBAFZBGHIZUFEZJUHBAGKI
    CDZEZJABLUGUJULUHUJUIGKIZUKEUGULAUIMUGUMBUKUGBNEBREZUMBFZABOBPUNGREUOQBGSTU
    AUBUCUDUE $.
    $( [2-Sep-2009] $)

  $( Nonnegative integers expressed as a set of upper integers. $)
  nn0uz $p |- NN0 = ( ZZ>= ` 0 ) $=
    ( vk cn0 cc0 cv cle wbr cz crab cuz nn0zrab wcel wceq 0z uzval ax-mp eqtr4i
    cfv ) BCADEFAGHZCIQZAJCGKSRLMACNOP $.
    $( [2-Sep-2005] $)

  $( Natural numbers expressed as a set of upper integers. $)
  nnuz $p |- NN = ( ZZ>= ` 1 ) $=
    ( vk cn c1 cv cle wbr cz crab cuz cfv nnzrab wcel wceq uzval ax-mp eqtr4i
    1z ) BCADEFAGHZCIJZAKCGLSRMQACNOP $.
    $( [2-Sep-2005] $)

  $( A natural number expressed as a member of a set of upper integers. $)
  elnnuz $p |- ( N e. NN <-> N e. ( ZZ>= ` 1 ) ) $=
    ( cn c1 cuz cfv nnuz eleq2i ) BCDEAFG $.
    $( [6-Jun-2006] $)

  $( A nonnegative integer expressed as a member a set of upper integers. $)
  elnn0uz $p |- ( N e. NN0 <-> N e. ( ZZ>= ` 0 ) ) $=
    ( cn0 cc0 cuz cfv nn0uz eleq2i ) BCDEAFG $.
    $( [6-Jun-2006] $)

  $( The upper integers starting from a natural are a subset of the naturals.
     (Contributed by Scott Fenton, 29-Jun-2013.) $)
  uznnssnn $p |- ( N e. NN -> ( ZZ>= ` N ) C_ NN ) $=
    ( cn wcel cuz cfv c1 wss elnnuz uzss sylbi nnuz syl6sseqr ) ABCZADEZFDEZBMA
    OCNOGAHFAIJKL $.
    $( [29-Jun-2013] $)

  ${
    $d m n M $.
    $( Restricted universal quantification in a set of upper integers. $)
    raluz $p |- ( M e. ZZ -> ( A. n e. ( ZZ>= ` M ) ph <->
               A. n e. ZZ ( M <_ n -> ph ) ) ) $=
      ( cz wcel cv cle wbr wi cuz cfv wa eluz1 imbi1d impexp syl6bb ralbidv2 )
      CDEZACBFZGHZAIZBCJKZDRSUBEZAISDEZTLZAIUDUAIRUCUEACSMNUDTAOPQ $.
      $( [9-Sep-2005] $)

    $( Restricted universal quantification in a set of upper integers. $)
    raluz2 $p |- ( A. n e. ( ZZ>= ` M ) ph <->
               ( M e. ZZ -> A. n e. ZZ ( M <_ n -> ph ) ) ) $=
      ( cuz cfv wral cz wcel cv cle wbr wi w3a eluz2 3anass bitri imbi1i impexp
      wa imbi2i bi2.04 3bitri ralbii2 r19.21v ) ABCDEZFCGHZCBIZJKZALZLZBGFUFUIB
      GFLAUJBUEGUGUEHZALUFUGGHZUHSZSZALZUFULUILZLZULUJLUKUNAUKUFULUHMUNCUGNUFUL
      UHOPQUOUFUMALZLUQUFUMARURUPUFULUHARTPUFULUIUAUBUCUFUIBGUDP $.
      $( [9-Sep-2005] $)

    $( Restricted existential quantification in a set of upper integers. $)
    rexuz $p |- ( M e. ZZ -> ( E. n e. ( ZZ>= ` M ) ph <->
               E. n e. ZZ ( M <_ n /\ ph ) ) ) $=
      ( cz wcel cv cle wbr wa cuz cfv eluz1 anbi1d anass syl6bb rexbidv2 ) CDEZ
      ACBFZGHZAIZBCJKZDQRUAEZAIRDEZSIZAIUCTIQUBUDACRLMUCSANOP $.
      $( [9-Sep-2005] $)

    $( Restricted existential quantification in a set of upper integers. $)
    rexuz2 $p |- ( E. n e. ( ZZ>= ` M ) ph <->
               ( M e. ZZ /\ E. n e. ZZ ( M <_ n /\ ph ) ) ) $=
      ( cuz cfv wrex cz wcel cv cle wbr wa eluz2 df-3an bitri anbi1i anass an12
      w3a 3bitri rexbii2 r19.42v ) ABCDEZFCGHZCBIZJKZALZLZBGFUDUGBGFLAUHBUCGUEU
      CHZALUDUEGHZLZUFLZALUKUGLZUJUHLZUIULAUIUDUJUFSULCUEMUDUJUFNOPUKUFAQUMUDUJ
      UGLLUNUDUJUGQUDUJUGROTUAUDUGBGUBO $.
      $( [9-Sep-2005] $)

    $( Double existential quantification in a set of upper integers. $)
    2rexuz $p |- ( E. m E. n e. ( ZZ>= ` m ) ph <->
                  E. m e. ZZ E. n e. ZZ ( m <_ n /\ ph ) ) $=
      ( cv cuz cfv wrex wex cz wcel cle wbr wa rexuz2 exbii df-rex bitr4i ) ACB
      DZEFGZBHRIJRCDKLAMCIGZMZBHTBIGSUABACRNOTBIPQ $.
      $( [3-Nov-2005] $)
  $}

  $( Second Peano postulate for a set of upper integers. $)
  peano2uz $p |- ( N e. ( ZZ>= ` M ) ->
                 ( N + 1 ) e. ( ZZ>= ` M ) ) $=
    ( cz wcel cle wbr w3a c1 caddc co cuz cfv simp1 peano2z 3ad2ant2 zre letrp1
    cr syl3an2 eluz2 syl3an1 3jca 3imtr4i ) ACDZBCDZABEFZGZUDBHIJZCDZAUHEFZGBAK
    LZDUHUKDUGUDUIUJUDUEUFMUEUDUIUFBNOUDARDZUEUFUJAPUEULBRDUFUJBPABQSUAUBABTAUH
    TUC $.
    $( [7-Sep-2005] $)

  ${
    peano2uzs.1 $e |- Z = ( ZZ>= ` M ) $.
    $( Second Peano postulate for a set of upper integers. $)
    peano2uzs $p |- ( N e. Z -> ( N + 1 ) e. Z ) $=
      ( c1 caddc co wcel cuz cfv peano2uz syl6eleqr eleq2s ) BEFGZCHBAIJZCBOHNO
      CABKDLDM $.
      $( [26-Dec-2013] $)
  $}

  $( Reversed second Peano axiom for upper integers. $)
  peano2uzr $p |- ( ( M e. ZZ /\
             N e. ( ZZ>= ` ( M + 1 ) ) ) -> N e. ( ZZ>= ` M ) ) $=
    ( cz wcel c1 caddc co cuz cfv wa cmin wceq cc eluzelz zcn ax-1cn npcan 3syl
    mpan2 adantl eluzp1m1 peano2uz syl eqeltrrd ) ACDZBAEFGZHIDZJZBEKGZEFGZBAHI
    ZUGUJBLZUEUGBCDBMDZULUFBNBOUMEMDULPBEQSRTUHUIUKDUJUKDABUAAUIUBUCUD $.
    $( [2-Jan-2006] $)

  ${
    $d j K $.  $d j k M $.  $d j k N $.
    $( Addition closure law for a set of upper integers. $)
    uzaddcl $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. NN0 ) ->
                   ( N + K ) e. ( ZZ>= ` M ) ) $=
      ( vj vk cn0 wcel caddc co cv wi cc0 c1 wa wceq cc syl eleq1d oveq2 imbi2d
      cuz cfv cz eluzelz zcn nn0cn ax-1cn addass mp3an3 syl2anr adantr peano2uz
      adantl eqeltrrd exp31 a2d addid1 ibir nn0indALT impcom ) AFGCBUAUBZGZCAHI
      ZVAGZVBCDJZHIZVAGZKVBCLHIZVAGZKVBCEJZHIZVAGZKVBCVJMHIZHIZVAGZKVBVDKDEAVJF
      GZVBVLVOVPVBVLVOVPVBNZVLNVKMHIZVNVAVQVRVNOZVLVBCPGZVJPGZVSVPVBCUCGVTBCUDC
      UEQZVJUFVTWAMPGVSUGCVJMUHUIUJUKVLVRVAGVQBVKULUMUNUOUPVBVIVBVHCVAVBVTVHCOW
      BCUQQRURVELOZVGVIVBWCVFVHVAVELCHSRTVEVJOZVGVLVBWDVFVKVAVEVJCHSRTVEVMOZVGV
      OVBWEVFVNVAVEVMCHSRTVEAOZVGVDVBWFVFVCVAVEACHSRTUSUT $.
      $( [4-Jun-2006] $)
  $}

  ${
    $d j m N $.  $d j ps $.  $d j ch $.  $d j th $.  $d j ta $.  $d k ph $.
    $d j k m M $.
    $( Substitutions. $)
    uzind4.1 $e |- ( j = M -> ( ph <-> ps ) ) $.
    uzind4.2 $e |- ( j = k -> ( ph <-> ch ) ) $.
    uzind4.3 $e |- ( j = ( k + 1 ) -> ( ph <-> th ) ) $.
    uzind4.4 $e |- ( j = N -> ( ph <-> ta ) ) $.
    $( Basis. $)
    uzind4.5 $e |- ( M e. ZZ -> ps ) $.
    $( Induction hypothesis. $)
    uzind4.6 $e |- ( k e. ( ZZ>= ` M ) -> ( ch -> th ) ) $.
    $( Induction on the set of upper integers that starts at an integer
       ` M ` .  The first four hypotheses give us the substitution instances we
       need, and the last two are the basis and the induction hypothesis. $)
    uzind4 $p |- ( N e. ( ZZ>= ` M ) -> ta ) $=
      ( vm wcel cz cle wbr cuz cfv cv crab eluzel2 eluzelz breq2 elrab sylanbrc
      eluzle wa wi w3a eluz2 biimpri 3expb sylan2b syl uzind3 syl2anc ) IHUAUBZ
      QZHRQZIHPUCZSTZPRUDZQZEHIUEVBIRQHISTZVGHIUFHIUJVEVHPIRVDIHSUGUHUIABCDEFPG
      HIJKLMNVCGUCZVFQZUKVIVAQZCDULVJVCVIRQZHVISTZUKVKVEVMPVIRVDVIHSUGUHVCVLVMV
      KVKVCVLVMUMHVIUNUOUPUQOURUSUT $.
      $( [7-Sep-2005] $)
  $}

  ${
    $d j N $.  $d j ps $.  $d j ch $.  $d j th $.  $d j ta $.  $d k ph $.
    $d j k M $.
    $( Basis. $)
    uzind4ALT.5 $e |- ( M e. ZZ -> ps ) $.
    $( Induction hypothesis. $)
    uzind4ALT.6 $e |- ( k e. ( ZZ>= ` M ) -> ( ch -> th ) ) $.
    $( Substitutions. $)
    uzind4ALT.1 $e |- ( j = M -> ( ph <-> ps ) ) $.
    uzind4ALT.2 $e |- ( j = k -> ( ph <-> ch ) ) $.
    uzind4ALT.3 $e |- ( j = ( k + 1 ) -> ( ph <-> th ) ) $.
    uzind4ALT.4 $e |- ( j = N -> ( ph <-> ta ) ) $.
    $( Alternate version of ~ uzind4 with different hypothesis order for easier
       use with the Metamath Proof Assistant, since "assign last" will assign
       the substitutions first.  (This may or may not be kept permanenently, or
       it may replace ~ uzind4 - I haven't decided yet. --NM) $)
    uzind4ALT $p |- ( N e. ( ZZ>= ` M ) -> ta ) $=
      ( uzind4 ) ABCDEFGHILMNOJKP $.
      $( [7-Sep-2005] $)
  $}

  ${
    $d m k j M $.  $d j N $.  $d j m ph $.
    $( Basis. $)
    uzind4s.1 $e |- ( M e. ZZ -> [ M / k ] ph ) $.
    $( Induction hypothesis. $)
    uzind4s.2 $e |- ( k e. ( ZZ>= ` M ) -> ( ph -> [ ( k + 1 ) / k ] ph ) ) $.
    $( Induction on the set of upper integers that starts at an integer ` M ` ,
       using explicit substitution.  The hypotheses are the basis and the
       induction hypothesis. $)
    uzind4s $p |- ( N e. ( ZZ>= ` M ) -> [ N / k ] ph ) $=
      ( vj vm cv wsbc c1 caddc co dfsbcq cuz wcel wi hbim wceq imbi12d cfv hbs1
      ax-17 ovex hbsbc1v eleq1 sbequ12 wb oveq1 syl chvar uzind4 ) ABGIZJABCJAB
      HIZJZABUNKLMZJZABDJGHCDABUMCNABUMUNNABUMUPNABUMDNEBIZCOUAZPZAABURKLMZJZQZ
      QUNUSPZUOUQQZQBHVDVEBVDBUCUOUQBABHUBABUPUNKLUDUERRURUNSZUTVDVCVEURUNUSUFV
      FAUOVBUQABHUGVFVAUPSVBUQUHURUNKLUIABVAUPNUJTTFUKUL $.
      $( [4-Nov-2005] $)
  $}

  ${
    $d k m n M $.  $d m N $.  $d k m n ph $.  $d j k m n $.
    $( Basis. $)
    uzind4s2.1 $e |- ( M e. ZZ -> [ M / j ] ph ) $.
    $( Induction hypothesis. $)
    uzind4s2.2 $e |- ( k e. ( ZZ>= ` M ) ->
                     ( [ k / j ] ph -> [ ( k + 1 ) / j ] ph ) ) $.
    $( Induction on the set of upper integers that starts at an integer ` M ` ,
       using explicit substitution.  The hypotheses are the basis and the
       induction hypothesis.  Use this instead of ~ uzind4s when ` j ` and
       ` k ` must be distinct in ` [ ( k + 1 ) / j ] ph ` . $)
    uzind4s2 $p |- ( N e. ( ZZ>= ` M ) -> [ N / j ] ph ) $=
      ( vm vn cv wsbc c1 caddc co dfsbcq wi cuz cfv wceq wb syl imbi12d vtoclga
      oveq1 uzind4 ) ABHJZKABDKABIJZKZABUGLMNZKZABEKHIDEABUFDOABUFUGOABUFUIOABU
      FEOFABCJZKZABUKLMNZKZPUHUJPCUGDQRUKUGSZULUHUNUJABUKUGOUOUMUISUNUJTUKUGLMU
      DABUMUIOUAUBGUCUE $.
      $( [16-Nov-2005] $)
  $}

  ${
    $d j m N $.  $d j ps $.  $d j ch $.  $d j th $.  $d j ta $.  $d k ph $.
    $d j k m M $.
    $( Lower bound. $)
    uzind4i.1 $e |- M e. ZZ $.
    $( Substitutions. $)
    uzind4i.2 $e |- ( j = M -> ( ph <-> ps ) ) $.
    uzind4i.3 $e |- ( j = k -> ( ph <-> ch ) ) $.
    uzind4i.4 $e |- ( j = ( k + 1 ) -> ( ph <-> th ) ) $.
    uzind4i.5 $e |- ( j = N -> ( ph <-> ta ) ) $.
    $( Basis. $)
    uzind4i.6 $e |- ps $.
    $( Induction hypothesis. $)
    uzind4i.7 $e |- ( k e. ( ZZ>= ` M ) -> ( ch -> th ) ) $.
    $( Induction on the upper integers that start at ` M ` .  The first
       hypothesis specifies the lower bound, the next four give us the
       substitution instances we need, and the last two are the basis and the
       induction hypothesis. $)
    uzind4i $p |- ( N e. ( ZZ>= ` M ) -> ta ) $=
      ( vm cv cz wcel cle wbr crab cuz cfv a1i uzval ax-mp eleq2i sylbir adantl
      wi wceq uzind3 mpan eleq2s ) EIHQRUAUBQSUCZHUDUEZHSTZIUQTEJABCDEFQGHIKLMN
      BUSOUFGRZUQTZCDULZUSVAUTURTVBURUQUTUSURUQUMJQHUGUHZUIPUJUKUNUOVCUP $.
      $( [4-Sep-2005] $)
  $}

  ${
    $d h t n m M $.  $d h j t k m n S $.
    $( Well-ordering principle: any non-empty subset of a set of upper integers
       has a least element. $)
    uzwo $p |- ( ( S C_ ( ZZ>= ` M ) /\ S =/= (/) ) ->
               E. j e. S A. k e. S j <_ k ) $=
      ( vt vn wa cv cle wbr wral wn wcel wceq wi breq1 ralbidv imbi2d cz wb cuz
      vh vm cfv wss c0 wne wrex wal c1 caddc co ssel eluzle ralrimiv adantr a1i
      syl6 uzssz sstr mpan2 eluzelz rcla4ev expcom con3rr3 cr zre letri3 syl2an
      ssel2 clt zleltp1 peano2re syl ltnle ancoms anbi2d sylan2 eleq1a ad2antll
      bitrd sylbird exp3a con1 com23 exp32 com34 imp41 ralimdva sylan9r pm2.43d
      expl sylani a2d uzind4 adantl sylcom adantrd pm2.61i alrimdv eq0 necon1ad
      ex syl6ibr imp breq2 cbvralv rexbii sylib ) ADUAUDZUEZAUFUGZGBHZEHZIJZEAK
      ZBAUHZXMCHZIJZCAKZBAUHXKXLXQXKXQAUFXKXQLZFHZAMZLZFUIAUFNXKYAYDFXKYAYDYBXJ
      MZXKYAGZYDOYEYFYBXNIJZEAKZYDYFUBHZXNIJZEAKZOYFDXNIJZEAKZOZYFUCHZXNIJZEAKZ
      OYFYOUJUKULZXNIJZEAKZOYFYHOUBUCDYBYIDNZYKYMYFUUAYJYLEAYIDXNIPQRYIYONZYKYQ
      YFUUBYJYPEAYIYOXNIPQRYIYRNZYKYTYFUUCYJYSEAYIYRXNIPQRYIYBNZYKYHYFUUDYJYGEA
      YIYBXNIPQRYNDSMXKYMYAXKYLEAXKXNAMZXNXJMYLAXJXNUMDXNUNURUOUPUQYOXJMZYFYQYT
      XKUUFASUEZYAYQYTOZXKXJSUEUUGDUSAXJSUTVAUUFYOSMZUUGYAGUUHODYOVBUUIUUGYAUUH
      UUIUUGGZYAGYQYTYAYQYOAMZLZUUJUUHYQUUKXQUUKYQXQXPYQBYOAXMYONXOYPEAXMYOXNIP
      QVCVDVEUUJUULUUHUUJUULGYPYSEAUUIUUGUULUUEYPYSOZUUIUUGUUEUULUUMUUIUUGUUEUU
      LUUMOUUIUUGUUEGZGZYPUULYSUUOYPYSLZUUKOUULYSOUUOYPUUPUUKUUOYPUUPGZYOXNNZUU
      KUUNUUIXNSMZUURUUQTASXNVJUUIUUSGZUURYPXNYOIJZGZUUQUUIYOVFMZXNVFMZUURUVBTU
      USYOVGZXNVGZYOXNVHVIUUTUVAUUPYPUUSUUIUVAUUPTUUSUUIGUVAXNYRVKJZUUPXNYOVLUU
      SUVDYRVFMZUVGUUPTUUIUVFUUIUVCUVHUVEYOVMVNXNYRVOVIWAVPVQWAVRUUEUURUUKOUUIU
      UGXNAYOVSVTWBWCYSUUKWDURWEWFWGWHWIXCWJWKWLVNWMWNWOYAYHYDOXKYHYCXQYCYHXQXP
      YHBYBAXMYBNXOYGEAXMYBXNIPQVCVDVEWPWQYELXKYDYAXKYCYEAXJYBUMVEWRWSXCWTFAXAX
      DXBXEXPXTBAXOXSECAXNXRXMIXFXGXHXI $.
      $( [17-Apr-2007] $) $( [8-Oct-2005] $)

    $( Well-ordering principle: any non-empty subset of the upper integers has
       a least element. $)
    uzwoOLD $p |- ( ( S C_ ( ZZ>= ` M ) /\ -. S = (/) ) ->
               E. j e. S A. k e. S j <_ k ) $=
      ( vt vn wceq wn wa cv cle wbr wral wcel wi breq1 ralbidv imbi2d cz wb cuz
      vh vm cfv wss c0 wrex wal c1 caddc ssel eluzle syl6 ralrimiv adantr uzssz
      a1i sstr mpan2 eluzelz rcla4ev expcom con3rr3 ssel2 zre letri3 syl2an clt
      co zleltp1 peano2re syl ltnle bitrd ancoms anbi2d sylan2 ad2antll sylbird
      eleq1a exp3a con1 com23 exp32 com34 imp41 ralimdva sylan9r pm2.43d sylani
      cr ex expl a2d uzind4 adantl sylcom adantrd pm2.61i alrimdv syl6ibr con1d
      eq0 imp breq2 cbvralv rexbii sylib ) ADUAUDZUEZAUFGZHZIBJZEJZKLZEAMZBAUGZ
      XMCJZKLZCAMZBAUGXJXLXQXJXQXKXJXQHZFJZANZHZFUHXKXJYAYDFXJYAYDYBXINZXJYAIZY
      DOYEYFYBXNKLZEAMZYDYFUBJZXNKLZEAMZOYFDXNKLZEAMZOZYFUCJZXNKLZEAMZOYFYOUIUJ
      VIZXNKLZEAMZOYFYHOUBUCDYBYIDGZYKYMYFUUAYJYLEAYIDXNKPQRYIYOGZYKYQYFUUBYJYP
      EAYIYOXNKPQRYIYRGZYKYTYFUUCYJYSEAYIYRXNKPQRYIYBGZYKYHYFUUDYJYGEAYIYBXNKPQ
      RYNDSNXJYMYAXJYLEAXJXNANZXNXINYLAXIXNUKDXNULUMUNUOUQYOXINZYFYQYTXJUUFASUE
      ZYAYQYTOZXJXISUEUUGDUPAXISURUSUUFYOSNZUUGYAIUUHODYOUTUUIUUGYAUUHUUIUUGIZY
      AIYQYTYAYQYOANZHZUUJUUHYQUUKXQUUKYQXQXPYQBYOAXMYOGXOYPEAXMYOXNKPQVAVBVCUU
      JUULUUHUUJUULIYPYSEAUUIUUGUULUUEYPYSOZUUIUUGUUEUULUUMUUIUUGUUEUULUUMOUUIU
      UGUUEIZIZYPUULYSUUOYPYSHZUUKOUULYSOUUOYPUUPUUKUUOYPUUPIZYOXNGZUUKUUNUUIXN
      SNZUURUUQTASXNVDUUIUUSIZUURYPXNYOKLZIZUUQUUIYOWKNZXNWKNZUURUVBTUUSYOVEZXN
      VEZYOXNVFVGUUTUVAUUPYPUUSUUIUVAUUPTUUSUUIIUVAXNYRVHLZUUPXNYOVJUUSUVDYRWKN
      ZUVGUUPTUUIUVFUUIUVCUVHUVEYOVKVLXNYRVMVGVNVOVPVNVQUUEUURUUKOUUIUUGXNAYOVT
      VRVSWAYSUUKWBUMWCWDWEWFWGWLWHWIWMVLWJWNWOYAYHYDOXJYHYCXQYCYHXQXPYHBYBAXMY
      BGXOYGEAXMYBXNKPQVAVBVCWPWQYEHXJYDYAXJYCYEAXIYBUKVCWRWSWLWTFAXCXAXBXDXPXT
      BAXOXSECAXNXRXMKXEXFXGXH $.
      $( [8-Oct-2005] $)

    $( Well-ordering principle: any non-empty subset of upper integers has a
       unique least element. $)
    uzwo2 $p |- ( ( S C_ ( ZZ>= ` M ) /\ S =/= (/) ) ->
               E! j e. S A. k e. S j <_ k ) $=
      ( cuz cfv wss c0 wne cv cle wbr wral wrex wreu uzwo cr cz uzssz zssre
      sstri sstr mpan2 lbreu sylan syldan ) ADEFZGZAHIBJCJKLCAMZBANZUIBAOZABCDP
      UHAQGZUJUKUHUGQGULUGRQDSTUAAUGQUBUCBCAUDUEUF $.
      $( [22-Apr-2007] $) $( [8-Oct-2005] $)
  $}

  ${
    $d x y A $.
    $( Well-ordering principle: any non-empty set of natural numbers has a
       least element.  Theorem I.37 (well-ordering principle) of [Apostol]
       p. 34. $)
    nnwo $p |- ( ( A C_ NN /\ A =/= (/) ) -> E. x e. A A. y e. A x <_ y ) $=
      ( cn wss c1 cuz cfv c0 wne cv cle wbr wral wrex nnuz sseq2i uzwo sylanb )
      CDECFGHZECIJAKBKLMBCNACODTCPQCABFRS $.
      $( [11-Nov-2004] $) $( [17-Aug-2001] $)
  $}

  ${
    $d x y z w v $.  $d w v z A $.
    nnwof.1 $e |- ( z e. A -> A. x z e. A ) $.
    nnwof.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Well-ordering principle: any non-empty set of natural numbers has a
       least element.  This version allows ` x ` and ` y ` to be present in
       ` A ` as long as they are effectively not free. $)
    nnwof $p |- ( ( A C_ NN /\ A =/= (/) ) -> E. x e. A A. y e. A x <_ y ) $=
      ( vw vv wa cv cle wbr wral wrex wcel wi wal wex hblem ax-17 cn wss c0 wne
      nnwo hbim wceq eleq1 breq2 imbi12d cbval anbi2i exbii breq1 imbi2d albidv
      hbal hban anbi12d cbvex bitri df-rex 3bitr4i df-ral rexbii sylib ) DUAUBD
      UCUDIGJZHJZKLZHDMZGDNZAJZBJZKLZBDMZADNZGHDUEVHDOZVIPZHQZGDNZVMDOZVNPZBQZA
      DNZVKVPVGDOZVSIZGRZVLDOZWCIZARZVTWDWGWEWAVGVMKLZPZBQZIZGRWJWFWNGVSWMWEVRW
      LHBVQVIBBCHDFSVIBTUFWLHTVHVMUGVQWAVIWKVHVMDUHVHVMVGKUIUJUKULUMWNWIGAWEWMA
      ACGDESWLABWAWKAACBDESWKATUFUQURWIGTVGVLUGZWEWHWMWCVGVLDUHWOWLWBBWOWKVNWAV
      GVLVMKUNUOUPUSUTVAVSGDVBWCADVBVCVJVSGDVIHDVDVEVOWCADVNBDVDVEVCVF $.
      $( [17-Aug-2001] $)
  $}

  ${
    $d x y z $.  $d y z ph $.  $d x ps $.
    nnwos.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Well-ordering principle: any non-empty set of natural numbers has a
       least element (schema form). $)
    nnwos $p |- ( E. x e. NN ph ->
                  E. x e. NN ( ph /\ A. y e. NN ( ps -> x <_ y ) ) ) $=
      ( vz cn wa cv wral wrex wi wcel bitr3i wex wal df-rex df-ral bitri exbii
      crab wss wne cle wbr hbrab1 ax-17 nnwof ssrab2 biantrur rabn0 rabid elrab
      c0 imbi1i impexp albii anbi12i anbi2i anass bitr4i 3bitri 3imtr3i ) ACGUA
      ZGUBZVDUNUCZHZCIZDIZUDUEZDVDJZCVDKZACGKZABVJLZDGJZHZCGKZCDFVDACFGUFFIVDMD
      UGUHVGVFVMVEVFACGUIUJACGUKNVLVHVDMZVKHZCOVHGMZAHZVIGMZVNLZDPZHZCOZVQVKCVD
      QVSWECVRWAVKWDACGULVKVIVDMZVJLZDPWDVJDVDRWHWCDWHWBBHZVJLWCWGWIVJABCVIGEUM
      UOWBBVJUPSUQSURTWFVTVPHZCOVQWEWJCWEWAVOHWJVOWDWAVNDGRUSVTAVOUTNTVPCGQVAVB
      VC $.
      $( [17-Aug-2001] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    $( Substitutions. $)
    indstr.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Induction hypothesis. $)
    indstr.2 $e |- ( x e. NN -> ( A. y e. NN ( y < x -> ps ) -> ph ) ) $.
    $( Strong Mathematical Induction for positive integers (inference
       schema). $)
    indstr $p |- ( x e. NN -> ph ) $=
      ( cn wral wn wrex cv cle wbr wi wa wcel pm3.24 clt cr nnre syl2an syl6bbr
      wb lenlt imbi2d con34b ralbidva sylbid anim2d ancom syl6ib mtoi nrex wceq
      notbid nnwos mto dfral2 mpbir rspec ) ACGACGHAIZCGJZIVBVABIZCKZDKZLMZNZDG
      HZOZCGJVICGVDGPZVIAVAOZAQVJVIVAAOVKVJVHAVAVJVHVEVDRMZBNZDGHAVJVGVMDGVJVEG
      PZOZVGVCVLIZNVMVOVFVPVCVJVDSPVESPVFVPUCVNVDTVETVDVEUDUAUEVLBUFUBUGFUHUIVA
      AUJUKULUMVAVCCDVDVEUNABEUOUPUQACGURUSUT $.
      $( [17-Aug-2001] $)
  $}

  $( Membership in a nonegative set of upper integers implies membership in
     ` NN0 ` .  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  eluznn0 $p |- ( ( N e. NN0 /\ M e. ( ZZ>= ` N ) ) -> M e. NN0 ) $=
    ( cc0 cn0 nn0uz uztrn2 ) CABDEF $.
    $( [22-Jun-2011] $)

  $( Two ways to say "an integer greater than or equal to 2."  (Contributed by
     Paul Chapman, 23-Nov-2012.) $)
  eluz2b1 $p |- ( N e. ( ZZ>= ` 2 ) <-> ( N e. ZZ /\ 1 < N ) ) $=
    ( c2 cuz cfv wcel cz cle wbr wa c1 clt 2z eluz1i caddc co zltp1le mpan df-2
    wb 1z breq1i syl6bbr pm5.32i bitr4i ) ABCDEAFEZBAGHZIUEJAKHZIBALMUEUGUFUEUG
    JJNOZAGHZUFJFEUEUGUISTJAPQBUHAGRUAUBUCUD $.
    $( [27-Nov-2012] $) $( [23-Nov-2012] $)

  $( Two ways to say "an integer greater than or equal to 2."  (Contributed by
     Paul Chapman, 23-Nov-2012.) $)
  eluz2b2 $p |- ( N e. ( ZZ>= ` 2 ) <-> ( N e. NN /\ 1 < N ) ) $=
    ( c2 cuz cfv wcel cz c1 clt wbr wa cn eluz2b1 cle cr 1re zre ltle imdistani
    wi sylancr elnnz1 sylibr simpr jca nnz anim1i impbii bitri ) ABCDEAFEZGAHIZ
    JZAKEZUJJZALUKUMUKULUJUKUIGAMIZJULUIUJUNUIGNEANEUJUNSOAPGAQTRAUAUBUIUJUCUDU
    LUIUJAUEUFUGUH $.
    $( [23-Nov-2012] $)

  $( Two ways to say "an integer greater than or equal to 2."  (Contributed by
     Paul Chapman, 23-Nov-2012.) $)
  eluz2b3 $p |- ( N e. ( ZZ>= ` 2 ) <-> ( N e. NN /\ N =/= 1 ) ) $=
    ( c2 cuz cfv wcel cn c1 clt wbr wa wne eluz2b2 nngt1ne1 pm5.32i bitri ) ABC
    DEAFEZGAHIZJPAGKZJALPQRAMNO $.
    $( [23-Nov-2012] $)

  $( One less than an integer greater than or equal to 2 is a positive
     integer.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
  uz2m1nn $p |- ( N e. ( ZZ>= ` 2 ) -> ( N - 1 ) e. NN ) $=
    ( c2 cuz cfv wcel cz c1 clt wbr wa cmin co cn eluz2b1 wb znnsub mpan biimpa
    1z sylbi ) ABCDEAFEZGAHIZJAGKLMEZANUAUBUCGFEUAUBUCOSGAPQRT $.
    $( [23-Nov-2012] $) $( [17-Nov-2012] $)

  $( 1 is not in ` ( ZZ>= `` 2 ) ` .  (Contributed by Paul Chapman,
     21-Nov-2012.) $)
  1nuz2 $p |- -. 1 e. ( ZZ>= ` 2 ) $=
    ( c1 c2 cuz cfv wcel wne neirr cn eluz2b3 simprbi mto ) ABCDEZAAFZAGLAHEMAI
    JK $.
    $( [23-Nov-2012] $) $( [21-Nov-2012] $)

  $( A positive integer is either 1 or greater than or equal to 2.
     (Contributed by Paul Chapman, 17-Nov-2012.) $)
  elnn1uz2 $p |- ( N e. NN <-> ( N = 1 \/ N e. ( ZZ>= ` 2 ) ) ) $=
    ( c1 wceq c2 cuz cfv wcel wo cn wne wa ordi eluz2b3 exmidne biantru 3bitr4i
    orbi2i wi 1nn eleq1 mpbiri pm2.621 ax-mp olc impbii bitr2i ) ABCZADEFGZHZUG
    AIGZHZUJUGUJABJZKZHUKUGULHZKUIUKUGUJULLUHUMUGAMQUNUKABNOPUKUJUGUJRUKUJRUGUJ
    BIGSABITUAUGUJUBUCUJUGUDUEUF $.
    $( [23-Nov-2012] $) $( [17-Nov-2012] $)

  $( Closure of multiplication of integers greater than or equal to 2.
     (Contributed by Paul Chapman, 26-Oct-2012.) $)
  uz2mulcl $p |- ( ( M e. ( ZZ>= ` 2 ) /\ N e. ( ZZ>= ` 2 ) ) ->
                   ( M x. N ) e. ( ZZ>= ` 2 ) ) $=
    ( c2 cuz cfv wcel wa cmul co cz c1 clt wbr eluzelz syl2an cr eluz2b1 anim1i
    zre sylbi zmulcl mulgt1 an4s sylanbrc ) ACDEZFZBUEFZGABHIZJFZKUHLMZUHUEFUFA
    JFZBJFZUIUGCANCBNABUAOUFAPFZKALMZGZBPFZKBLMZGZUJUGUFUKUNGUOAQUKUMUNASRTUGUL
    UQGURBQULUPUQBSRTUMUPUNUQUJABUBUCOUHQUD $.
    $( [23-Nov-2012] $) $( [26-Oct-2012] $)

  ${
    $d ph y $.  $d ps x $.  $d x y $.
    indstr2.1 $e |- ( x = 1 -> ( ph <-> ch ) ) $.
    indstr2.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    indstr2.3 $e |- ch $.
    indstr2.4 $e |- ( x e. ( ZZ>= ` 2 ) ->
                      ( A. y e. NN ( y < x -> ps ) -> ph ) ) $.
    $( Strong Mathematical Induction for positive integers (inference schema).
       The first two hypotheses give us the substitution instances we need; the
       last two are the basis and the induction hypothesis.  (Contributed by
       Paul Chapman, 21-Nov-2012.) $)
    indstr2 $p |- ( x e. NN -> ph ) $=
      ( cv cn wcel c1 wceq c2 clt wbr wi wb cr cuz wo wral elnn1uz2 wa wn nnge1
      cfv cle 1re nnre lenlt sylancr mpbid adantl breq2 adantr mtbird ralrimiva
      pm2.21d pm5.5 syl bitrd mpbiri jaoi sylbi indstr ) ABDEGDJZKLVHMNZVHOUAUH
      LZUBEJZVHPQZBRZEKUCZARZVHUDVIVOVJVIVOCHVIVOACVIVNVOASVIVMEKVIVKKLZUEZVLBV
      QVLVKMPQZVPVRUFZVIVPMVKUIQZVSVKUGVPMTLVKTLVTVSSUJVKUKMVKULUMUNUOVIVLVRSVP
      VHMVKPUPUQURUTUSVNAVAVBFVCVDIVEVFVG $.
      $( [21-Nov-2012] $)
  $}

  ${
    $d j k M $.
    uzinfm.1 $e |- M e. ZZ $.
    $( Extract the lower bound of a set of upper integers as its infimum.  Note
       that the " ` ``' < ` " argument turns supremum into infimum (for which
       we do not currently have a separate notation). $)
    uzinfmi $p |- sup ( ( ZZ>= ` M ) , RR , `' < ) = M $=
      ( vj vk cuz cfv cr clt ccnv csup cv cle wral crab cuni wceq cz wcel mp2an
      wbr wrex uzssz zssre sstri uzid ax-mp eluzle breq1 ralbidv rcla4ev lbinfm
      wss rgen wreu wb lbreu reuuni2 mpbi eqtri ) AEFZGHIJZCKZDKZLTZDUTMZCUTNOZ
      AUTGULZVECUTUAZVAVFPUTQGAUBUCUDZAUTRZAVCLTZDUTMZVHAQRVJBAUEUFZVKDUTAVCUGU
      MZVEVLCAUTVBAPVDVKDUTVBAVCLUHUIZUJSZCDUTUKSVLVFAPZVNVJVECUTUNZVLVQUOVMVGV
      HVRVIVPCDUTUPSVEVLCUTAVOUQSURUS $.
      $( [7-Oct-2005] $)
  $}

  $( The infimum of the set of natural numbers is one.  Note
     that " ` ``' < ` " turns sup into inf. $)
  nninfm $p |- sup ( NN , RR , `' < ) = 1 $=
    ( cn cr clt ccnv csup c1 cuz cfv nnuz supeq1i 1z uzinfmi eqtri ) ABCDZEFGHZ
    BNEFBAONIJFKLM $.
    $( [16-Jun-2005] $)

  $( The infimum of the set of nonnegative integers is zero.  Note
     that " ` ``' < ` " turns sup into inf. $)
  nn0infm $p |- sup ( NN0 , RR , `' < ) = 0 $=
    ( cn0 cr clt ccnv csup cc0 cuz cfv nn0uz supeq1i 0z uzinfmi eqtri ) ABCDZEF
    GHZBNEFBAONIJFKLM $.
    $( [16-Jun-2005] $)

  ${
    $d j k S $.  $d k A $.
    $( The infimum of a subset of a set of upper integers is less than or equal
       to all members of the subset.  Note that the " ` ``' < ` " argument
       turns supremum into infimum (for which we do not currently have a
       separate notation). $)
    infmssuzle $p |- ( ( S C_ ( ZZ>= ` M ) /\ A e. S ) ->
                sup ( S , RR , `' < ) <_ A ) $=
      ( vj vk cuz cfv wss wcel cv cle wbr wral wrex cr clt ccnv csup c0 wne cz
      ne0i uzwo sylan2 uzssz zssre sstri mpan2 lbinfmle 3com23 syl3an1 mpd3an3
      sstr ) BCFGZHZABIZDJEJKLEBMDBNZBOPQRAKLZUPUOBSTUQBAUBBDECUCUDUOBOHZUPUQUR
      UOUNOHUSUNUAOCUEUFUGBUNOUMUHUSUQUPURDEABUIUJUKUL $.
      $( [29-Jun-2011] $) $( [11-Oct-2005] $)

    $( The infimum of a subset of a set of upper integers belongs to the
       subset. $)
    infmssuzcl $p |- ( ( S C_ ( ZZ>= ` M ) /\ S =/= (/) ) ->
                  sup ( S , RR , `' < ) e. S ) $=
      ( vj vk cuz cfv wss c0 wne wa cr cv cle wbr wral wrex clt ccnv csup wcel
      cz uzssz zssre sstri sstr mpan2 adantr uzwo lbinfmcl syl2anc ) ABEFZGZAHI
      ZJAKGZCLDLMNDAOCAPAKQRSATULUNUMULUKKGUNUKUAKBUBUCUDAUKKUEUFUGACDBUHCDAUIU
      J $.
      $( [10-May-2007] $) $( [11-Oct-2005] $)
  $}

  ${
    $d A a b x y z $.
    $( The image under negation of a bounded-above set of reals is bounded
       below.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    ublbneg $p |- ( E. x e. RR A. y e. A y <_ x ->
                    E. x e. RR A. y e. { z e. RR | -u z e. A } x <_ y ) $=
      ( vb va cv cle wbr wral cr wrex cneg wcel crab breq1 cbvralv wceq ralbidv
      rexbii breq2 cbvrexv bitri wa wi ssrab2 sseli eleq1d elrab3 biimpd rcla4v
      negeq mpcom syl adantl lenegcon1 sylan2 sylibrd ralrimdva renegcl rcla4ev
      wb sylan ex syld rexlimiv sylbir ) BGZAGZHIZBDJZAKLZEGZFGZHIZEDJZFKLZVIVH
      HIZBCGZMZDNZCKOZJZAKLZVQVHVNHIZBDJZFKLVLVPWFFKVOWEEBDVMVHVNHPQTWFVKFAKVNV
      IRWEVJBDVNVIVHHUASUBUCVPWDFKVNKNZVPVNMZVHHIZBWBJZWDWGVPWIBWBWGVHWBNZUDVPV
      HMZVNHIZWIWKVPWMUEZWGWKWLDNZWNVHKNZWKWOWBKVHWACKUFUGZWPWKWOWAWOCVHKVSVHRV
      TWLDVSVHULUHUIUJUMVOWMEWLDVMWLVNHPUKUNUOWKWGWPWIWMVBWQVNVHUPUQURUSWGWJWDW
      GWHKNWJWDVNUTWCWJAWHKVIWHRVRWIBWBVIWHVHHPSVAVCVDVEVFVG $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d A w z $.
    $( Two ways to express the image under negation of a set of integers.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    eqreznegel $p |- ( A C_ ZZ ->
                       { z e. RR | -u z e. A } = { z e. ZZ | -u z e. A } ) $=
      ( vw cz wss cv cneg wcel cr crab wa wi ssel cc recn caddc cc0 negid elrab
      co 0z syl6eqel pm4.71i zrevaddcl syl5bb syl5ib syl6 com23 imp3a simpr a1i
      jcad zre anim1i impbid1 wceq negeq eleq1d 3bitr4g eqrdv ) BDEZCAFZGZBHZAI
      JZVDADJZVACFZIHZVGGZBHZKZVGDHZVJKZVGVEHVGVFHVAVKVMVAVKVLVJVAVHVJVLVAVJVHV
      LVAVJVIDHZVHVLLBDVIMVHVGNHZVNVLVGOVOVOVGVIPTZDHZKVNVLVOVQVOVPQDVGRUAUBUCV
      GVIUDUEUFUGUHUIVKVJLVAVHVJUJUKULVLVHVJVGUMUNUOVDVJAVGIVBVGUPVCVIBVBVGUQUR
      ZSVDVJAVGDVRSUSUT $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d A w x y z $.
    $( The image under negation of a nonempty set of reals is nonempty.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    negn0 $p |- ( ( A C_ RR /\ A =/= (/) ) ->
                  { z e. RR | -u z e. A } =/= (/) ) $=
      ( vx vy cr wss c0 wne cv cneg wcel crab wex n0 ssel wb renegcl eleq1d syl
      wceq negeq elrab3 cc recn negneg bitrd biimprd syli elex2 syl6ibr exlimdv
      syl6 syl5bi imp ) BEFZBGHZAIZJZBKZAELZGHZUPCIZBKZCMUOVACBNUOVCVACUOVCDIUT
      KDMZVAUOVCVBJZUTKZVDVCUOVBEKZVFBEVBOVGVFVCVGVFVEJZBKZVCVGVEEKVFVIPVBQUSVI
      AVEEUQVETURVHBUQVEUARUBSVGVHVBBVGVBUCKVHVBTVBUDVBUESRUFUGUHDVEUTUIULDUTNU
      JUKUMUN $.
      $( [21-Mar-2011] $)

    $( The supremum of a bounded-above set of reals is the negation of the
       supremum of that set's image under negation.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    supminf $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
      sup ( A , RR , < ) = -u sup ( { z e. RR | -u z e. A } , RR , `' < ) ) $=
      ( vw cr wss c0 wne cv cneg wcel clt csup wceq syl2an eleq1d wb cc recn wa
      cle wbr wral wrex w3a crab ccnv negn0 ublbneg ssrab2 infmsup mp3an1 3impa
      negeq elrab simplbi adantl ssel2 elrab3 renegcl syl negneg 3bitrd supeq1d
      eqrdav 3ad2ant1 negeqd eqtrd infmrcl suprcl negcon2 syl2anc mpbid ) DFGZD
      HIZBJZAJZUBUCBDUDAFUEZUFZCJZKZDLZCFUGZFMUHNZDFMNZKZOZWFWEKOZVTWEEJZKZWDLZ
      EFUGZFMNZKZWGVOVPVSWEWOOZVOVPUAZWDHIZVRVQUBUCBWDUDAFUEZWPVSCDUIZABCDUJZWD
      FGZWRWSWPWCCFUKZABEWDULUMPUNVTWNWFVOVPWNWFOVSVOFWMDMVOAWMDFVRWMLZVRFLZVOX
      DXEVRKZWDLZWLXGEVRFWJVROWKXFWDWJVRUOQZUPUQURDFVRUSXEXDVRDLZRVOXEXDXGXFKZD
      LZXIWLXGEVRFXHUTXEXFFLXGXKRVRVAWCXKCXFFWAXFOWBXJDWAXFUOQUTVBXEXJVRDXEVRSL
      XJVROVRTVRVCVBQVDURVFVEVGVHVIVTWEFLZWFFLZWHWIRZVOVPVSXLWQWRWSXLVSWTXAXBWR
      WSXLXCABWDVJUMPUNABDVKXLWESLWFSLXNXMWETWFTWEWFVLPVMVN $.
      $( [21-Mar-2011] $)

    $( If a set of reals is bounded below, it is bounded below by an integer.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    lbzbi $p |- ( A C_ RR -> ( E. x e. RR A. y e. A x <_ y <->
                               E. x e. ZZ A. y e. A x <_ y ) ) $=
      ( vz cr wss cv cle wbr wral wrex cz wcel wi wa clt w3a expdimp com23 ex
      ax-17 hbre1 btwnz simpld ssel2 zre ltletr ltle 3adant2 syld syl3an1 exp3a
      3expia syl5 imp ralrimiv ralim anasss expcom imdistand wceq breq1 ralbidv
      syl rcla4ev syl6 ancomsd rexlimdv mpdi rexlimd zssre ssrexv ax-mp impbid1
      ) CEFZAGZBGZHIZBCJZAEKZVSALKZVOVSWAAEVOAUAVSALUBVOVSVPEMZWAVOVSWBWANVOVSO
      ZWBDGZVPPIZDLKZWAWBWFVPWDPIDLKDDVPUCUDWBWCWFWANZWBVOVSWGWBVOOZVSOWEWADLWH
      VSWDLMZWEWANZWHWIVSWJWHWEWIVSOZWAWHWEWKWANWHWEOZWKWIWDVQHIZBCJZOWAWLWIVSW
      NWHWEWIVSWNNZNWHWIWEWOWIWHWEWONZWIWBVOWPWIWBOZVOOZWEWOWRWEOZVRWMNZBCJWOWS
      WTBCWRWEVQCMZWTNWRXAWEWTWQVOXAWEWTNZVOXAOVQEMZWQXBCEVQUEWIWBXCXBWIWBXCQWE
      VRWMWIWDEMZWBXCWEVROZWMNWDUFXDWBXCQXEWDVQPIZWMWDVPVQUGXDXCXFWMNWBWDVQUHUI
      UJUKULUMUNRSUOUPVRWMBCUQVDTURUSSUOUTVSWNAWDLVPWDVAVRWMBCVPWDVQHVBVCVEVFTS
      VGRVHURUSVITSVJLEFWAVTNVKVSALEVLVMVN $.
      $( [21-Mar-2011] $)

    $( The supremum of a bounded-above set of integers is a member of the set.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    suprzcl $p |- ( ( A C_ ZZ /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                    sup ( A , RR , < ) e. A ) $=
      ( vz vw cz wss c0 wne cv cle wbr cr wrex w3a cneg wcel crab wceq wa zssre
      wral clt csup ccnv mpan2 supminf syl3an1 ublbneg ssrab2 lbzbi ax-mp sylib
      sstr wb cuz cfv simp2 a1d eqreznegel sseq1d mpbiri sseld 3ad2ant1 rcla4cv
      wi breq2 3ad2ant3 3jcad eluz biimp3ar syl6 ssrdv 3expb negn0 sylan adantr
      adantlr infmssuzcl syl2anc exp32 rexlimdv 3impia syl3an3 cbvrabv syl6eleq
      negeq eleq1d elrab simprbi syl eqeltrd ) CFGZCHIZBJZAJZKLBCUBAMNZOZCMUCUD
      ZDJZPZCQZDMRZMUCUEUDZPZCWMCMGZWNWQWSXESWMFMGXFUACFMUNUFZABDCUGUHWRXDWPPZC
      QZAMRZQZXECQZWRXDXCXJWQWMWNWPWOKLZBXCUBZAFNZXDXCQZWQXNAMNZXOABDCUIXCMGXQX
      OUOXBDMUJABXCUKULUMWMWNXOXPWMWNTZXNXPAFXRWPFQZXNXPXRXSXNTZTXCWPUPUQZGZXCH
      IZXPWMXTYBWNWMXSXNYBWMXSXNOZEXCYAYDEJZXCQZXSYEFQZWPYEKLZOYEYAQZYDYFXSYGYH
      YDXSYFWMXSXNURUSWMXSYFYGVFXNWMXCFYEWMXCFGXBDFRZFGXBDFUJWMXCYJFDCUTVAVBVCV
      DXNWMYFYHVFXSXMYHBYEXCWOYEWPKVGVEVHVIXSYGYIYHWPYEVJVKVLVMVNVRXRYCXTWMXFWN
      YCXGDCVOVPVQXCWPVSVTWAWBWCWDXBXIDAMWTWPSXAXHCWTWPWGWHWEWFXKXDMQXLXIXLAXDM
      WPXDSXHXECWPXDWGWHWIWJWKWL $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x y z M $.  $d w x y z Z $.
    uzsupss.1 $e |- Z = ( ZZ>= ` M ) $.
    $( Any bounded subset of upper integers has a supremum.  (Contributed by
       Mario Carneiro, 22-Jul-2014.) $)
    uzsupss $p |- ( ( M e. ZZ /\ A C_ Z /\ E. x e. RR A. y e. A y <_ x ) ->
                    E. x e. Z ( A. y e. A -. x < y /\
                      A. y e. Z ( y < x -> E. z e. A y < z ) ) ) $=
      ( vw cz wcel cv wbr wral cr wrex clt wn wa c0 wceq wss cle w3a wi cuz cfv
      simpl1 uzid syl6eleqr ral0 simpr raleqdv mpbiri eluzle wb eluzel2 eluzelz
      syl zre lenlt syl2an syl2anc mpbid eleq2s pm2.21d rgen a1i notbid ralbidv
      breq1 breq2 imbi1d anbi12d rcla4ev syl12anc wne csup simpl2 uzssz eqsstri
      syl6ss simpl3 suprzcl syl3anc sseldd zssre suprub sselda adantr ralrimiva
      sylan cbvralv sylib sstri sseli suprlub biimpd rexbidv imbi12d pm2.61dane
      3jca sylan2 ) EIJZDFUAZBKZAKZUBLBDMANOZUCZXFXEPLZQZBDMZXEXFPLZXECKZPLZCDO
      ZUDZBFMZRZAFOZDSXHDSTZRZEFJEXEPLZQZBDMZXEEPLZXOUDZBFMZXSYAEEUEUFZFYAXCEYH
      JXCXDXGXTUGEUHURGUIYAYDYCBSMYCBUJYAYCBDSXHXTUKULUMYGYAYFBFXEFJYEXOYEQZXEY
      HFXEYHJZEXEUBLZYIEXEUNYJXCXEIJZYKYIUOZEXEUPEXEUQXCENJXENJYMYLEUSXEUSEXEUT
      VAVBVCGVDVEVFVGXRYDYGRAEFXFETZXKYDXQYGYNXJYCBDYNXIYBXFEXEPVJVHVIYNXPYFBFY
      NXLYEXOXFEXEPVKVLVIVMVNVOXHDSVPZRZDNPVQZFJYQXEPLZQZBDMZXEYQPLZXOUDZBFMZXS
      YPDFYQXCXDXGYOVRZYPDIUAYOXGYQDJYPDFIUUDFYHIGEVSVTZWAZXHYOUKZXCXDXGYOWBZAB
      DWCWDZWEYPYQXMPLZQZCDMYTYPUUKCDYPXMDJZRZXMYQUBLZUUKYPDNUAZYOXGUCZUULUUNYP
      UUOYOXGYPDINUUFWFWAZUUGUUHXAZABDXMWGWKUUMXMNJYQNJZUUNUUKUOYPDNXMUUQWHYPUU
      SUULYPDNYQUUQUUIWEWIXMYQUTVBVCWJUUKYSCBDXMXETUUJYRXMXEYQPVKVHWLWMYPHKZYQP
      LZUUTXMPLZCDOZUDZHFMUUCYPUVDHFUUTFJYPUUTNJZUVDFNUUTFINUUEWFWNWOYPUVERUVAU
      VCYPUUPUVEUVAUVCUOUURABCDUUTWPWKWQXBWJUVDUUBHBFUUTXETZUVAUUAUVCXOUUTXEYQP
      VJUVFUVBXNCDUUTXEXMPVJWRWSWLWMXRYTUUCRAYQFXFYQTZXKYTXQUUCUVGXJYSBDUVGXIYR
      XFYQXEPVJVHVIUVGXPUUBBFUVGXLUUAXOXFYQXEPVKVLVIVMVNVOWT $.
      $( [22-Jul-2014] $)
  $}

