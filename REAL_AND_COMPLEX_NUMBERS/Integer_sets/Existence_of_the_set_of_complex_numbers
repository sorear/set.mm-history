$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Existence of the set of complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F k n x y $.  $d T n y $.
    rpnnen1.1 $e |- T = { n e. ZZ | ( n / k ) < x } $.
    rpnnen1.2 $e |- F = ( x e. RR |->
        ( k e. NN |-> ( sup ( T , RR , < ) / k ) ) ) $.
    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem1 $p |- ( x e. RR -> ( F ` x ) e. ( QQ ^m NN ) ) $=
      ( vy cv cr wcel cn clt co cq cz wa wbr c0 wrex csup cdiv cmpt cmap fvmpt2
      cfv cvv wceq nnex mptex mpan2 wf crab ssrab2 eqsstri wss wne cle wral a1i
      cmul nnre ax-mulrcl ancoms sylan2 btwnz simpld syl wb adantl simpll nngt0
      cc0 zre jca ad2antlr ltdivmul syl3anc rexbidva mpbird rabn0 sylibr neeq1i
      rabeq2i wi syl2anc ltle sylbid impr sylan2b breq2 ralbidv rcla4ev suprzcl
      ralrimiva sseldi znq sylancom eqid fmptd qex elmap eqeltrd ) AIZJKZXDEUFZ
      CLBJMUAZCIZUBNZUCZOLUDNZXEXJUGKXFXJUHCLXIUIUJAJXJUGEGUEUKXELOXJULXJXKKXEC
      LXIOXJXEXHLKZXGPKXIOKXEXLQZBPXGBDIZXHUBNXDMRZDPUMZPFXODPUNUOZXMBPUPZBSUQZ
      XNHIZURRZDBUSZHJTZXGBKXRXMXQUTXMXPSUQZXSXMXODPTZYDXMYEXNXHXDVANZMRZDPTZXM
      YFJKZYHXLXEXHJKZYIXHVBZYJXEYIXHXDVCZVDVEZYIYHYFXNMRDPTDDYFVFVGVHXMXOYGDPX
      MXNPKZQZXNJKZXEYJVMXHMRZQZXOYGVIYNYPXMXNVNVJZXEXLYNVKZXLYRXEYNXLYJYQYKXHV
      LVOVPXNXDXHVQVRZVSVTXODPWAWBBXPSFWCWBXMYIXNYFURRZDBUSZYCYMXMUUBDBXNBKXMYN
      XOQUUBXODBPFWDXMYNXOUUBYOXOYGUUBUUAYOYPYIYGUUBWEYSYOYJXEYIXLYJXEYNYKVPYTY
      LWFXNYFWGWFWHWIWJWOYBUUCHYFJXTYFUHYAUUBDBXTYFXNURWKWLWMWFHDBWNVRWPXGXHWQW
      RXJWSWTOLXJXAUIXBWBXC $.
      $( [12-May-2013] $)


    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem2 $p |- ( ( x e. RR /\ k e. NN ) -> sup ( T , RR , < ) e. ZZ ) $=
      ( vy cv cr wcel wa cz clt co wbr c0 cle wrex syl2anc cn csup cdiv eqsstri
      crab ssrab2 wss wne wral a1i cmul nnre ax-mulrcl ancoms sylan2 simpld syl
      btwnz cc0 wb zre adantl simpll nngt0 jca ad2antlr ltdivmul syl3anc mpbird
      rexbidva sylibr neeq1i rabeq2i wi ltle sylbid impr sylan2b ralrimiva wceq
      rabn0 breq2 ralbidv rcla4ev suprzcl sseldi ) AIZJKZCIZUAKZLZBMBJNUBZBDIZW
      IUCOWGNPZDMUEZMFWNDMUFUDZWKBMUGZBQUHZWMHIZRPZDBUIZHJSZWLBKWQWKWPUJWKWOQUH
      ZWRWKWNDMSZXCWKXDWMWIWGUKOZNPZDMSZWKXEJKZXGWJWHWIJKZXHWIULZXIWHXHWIWGUMZU
      NUOZXHXGXEWMNPDMSDDXEURUPUQWKWNXFDMWKWMMKZLZWMJKZWHXIUSWINPZLZWNXFUTXMXOW
      KWMVAVBZWHWJXMVCZWJXQWHXMWJXIXPXJWIVDVEVFWMWGWIVGVHZVJVIWNDMWAVKBWOQFVLVK
      WKXHWMXERPZDBUIZXBXLWKYADBWMBKWKXMWNLYAWNDBMFVMWKXMWNYAXNWNXFYAXTXNXOXHXF
      YAVNXRXNXIWHXHWJXIWHXMXJVFXSXKTWMXEVOTVPVQVRVSXAYBHXEJWSXEVTWTYADBWSXEWMR
      WBWCWDTHDBWEVHWF $.
      $( [12-May-2013] $)

    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem3 $p |- ( x e. RR -> A. n e. ran ( F ` x ) n <_ x ) $=
      ( vy cv cr wcel cle wbr wral cn wa clt co cvv cz csup cdiv cmpt wceq nnex
      cfv crn mptex fvmpt2 mpan2 fveq1d ovex eqid sylan9eq cmul rabeq2i cc0 zre
      wb adantl simpll nngt0 jca ad2antlr ltdivmul syl3anc wi ax-mulrcl syl2anc
      nnre ltle sylbid impr sylan2b ralrimiva wss c0 wne wrex crab ssrab2 zssre
      eqsstri sstri a1i ancoms sylan2 btwnz simpld rexbidva mpbird rabn0 sylibr
      neeq1i breq2 ralbidv rcla4ev suprleub syl31anc rpnnen1lem2 simpl ledivmul
      syl eqbrtrd wfn cq wf cmap rpnnen1lem1 qex elmap sylib ffn breq1 ralrn )
      AIZJKZDIZXPLMZDXPEUFZUGNZCIZXTUFZXPLMZCONZXQYDCOXQYBOKZPZYCBJQUAZYBUBRZXP
      LXQYFYCYBCOYIUCZUFZYIXQYBXTYJXQYJSKXTYJUDCOYIUEUHAJYJSEGUIUJUKYFYISKYKYIU
      DYHYBUBULCOYISYJYJUMUIUJUNYGYIXPLMZYHYBXPUORZLMZYGYNXRYMLMZDBNZYGYODBXRBK
      YGXRTKZXRYBUBRXPQMZPYOYRDBTFUPYGYQYRYOYGYQPZYRXRYMQMZYOYSXRJKZXQYBJKZUQYB
      QMZPZYRYTUSYQUUAYGXRURUTZXQYFYQVAZYFUUDXQYQYFUUBUUCYBVJZYBVBVCZVDXRXPYBVE
      VFZYSUUAYMJKZYTYOVGUUEYSUUBXQUUJYFUUBXQYQUUGVDUUFYBXPVHZVIXRYMVKVIVLVMVNV
      OZYGBJVPZBVQVRZXRHIZLMZDBNZHJVSZUUJYNYPUSUUMYGBTJBYRDTVTZTFYRDTWAWCWBWDWE
      YGUUSVQVRZUUNYGYRDTVSZUUTYGUVAYTDTVSZYGUUJUVBYFXQUUBUUJUUGUUBXQUUJUUKWFWG
      ZUUJUVBYMXRQMDTVSDDYMWHWIXCYGYRYTDTUUIWJWKYRDTWLWMBUUSVQFWNWMYGUUJYPUURUV
      CUULUUQYPHYMJUUOYMUDUUPYODBUUOYMXRLWOWPWQVIUVCHDDBYMWRWSWKYGYHJKZXQUUDYLY
      NUSYGYHTKUVDABCDEFGWTYHURXCXQYFXAYFUUDXQUUHUTYHXPYBXBVFWKXDVOXQXTOXEZYAYE
      USXQOXFXTXGZUVEXQXTXFOXHRKUVFABCDEFGXIXFOXTXJUEXKXLOXFXTXMXCXSYDDCOXTXRYC
      XPLXNXOXCWK $.
      $( [12-May-2013] $)

    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem4 $p |- ( x e. RR -> sup ( ran ( F ` x ) , RR , < ) e. RR ) $=
      ( vy cv cr wcel c0 wne cle wbr wral cn cq sylib syl cfv crn wss wrex csup
      clt wf cmap co rpnnen1lem1 qex nnex elmap frn qssre syl6ss cdm ne0i ax-mp
      c1 1nn fdm neeq1d mpbiri dm0rn0 necon3bii rpnnen1lem3 weq ralbidv rcla4ev
      breq2 mpdan suprcl syl3anc ) AIZJKZVOEUAZUBZJUCZVRLMZDIZHIZNOZDVRPZHJUDZV
      RJUFUEJKVPQRVQUGZVSVPVQRQUHUIKWFABCDEFGUJRQVQUKULUMSZWFVRRJQRVQUNUOUPTVPW
      FVTWGWFVQUQZLMZVTWFWIQLMZUTQKWJVAQUTURUSWFWHQLQRVQVBVCVDWHLVRLVQVEVFSTVPW
      AVONOZDVRPZWEABCDEFGVGWDWLHVOJHAVHWCWKDVRWBVOWANVKVIVJVLHDVRVMVN $.
      $( [12-May-2013] $)


    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem5 $p |- ( x e. RR -> sup ( ran ( F ` x ) , RR , < ) = x ) $=
      ( vy cr wcel clt cle wbr wa wb c0 cn co syl cz cv cfv csup wn rpnnen1lem4
      crn wceq eqlelt mpancom wral rpnnen1lem3 wss wne wrex cq cmap rpnnen1lem1
      wf qex nnex elmap sylib frn qssre syl6ss cdm c1 1nn ne0i ax-mp fdm neeq1d
      mpbiri dm0rn0 necon3bii breq2 ralbidv rcla4ev id suprleub syl31anc mpbird
      cmin cdiv cc0 resubcl adantr posdif biimpa gt0ne0 syl2anc rereccl arch ex
      mpdan w3a caddc rpnnen1lem2 zre 3adant3 ltp1 jca nnre nngt0 ltrec1 syl2an
      wi ad2antrr nnrecre adantl simpll ltaddsub2 syl3anc 3jca wfn ffn fnfvelrn
      sylan suprub sseldd leadd1 mpbid readdcl simpl lelttr exp3a mpd adantlr
      sylbird sylbid peano2zdi breq1d a1i ad2antlr sylibr cvv fvmpt2 mpan2 syld
      cc oveq1 elrab2 biimpri crab ssrab2 eqsstri zssre ax-mulrcl ancoms sylan2
      sstri cmul btwnz simpld ltdivmul rexbidva rabn0 rabeq2i ltle impr sylan2b
      neeq1i ralrimiva syldan recnd ax-1cn nncn nnne0 divdir cmpt ovex sylan9eq
      mptex fveq1d eqid oveq1d eqtr4d lenlt 3imtr3d exp31 com4l com14 3imp mt2d
      3exp rexlimdv pm2.01d mpbir2and ) AUAZIJZUWIEUBZUFZIKUCZUWIUGZUWMUWILMZUW
      MUWIKMZUDZUWMIJZUWJUWNUWOUWQNOABCDEFGUEZUWMUWIUHUIUWJUWODUAZUWILMZDUWLUJZ
      ABCDEFGUKZUWJUWLIULZUWLPUMZUWTHUAZLMZDUWLUJZHIUNZUWJUWOUXBOUWJQUOUWKURZUX
      DUWJUWKUOQUPRJUXJABCDEFGUQUOQUWKUSUTVAVBZUXJUWLUOIQUOUWKVCVDVESZUWJUXJUXE
      UXKUXJUWKVFZPUMZUXEUXJUXNQPUMZVGQJUXOVHQVGVIVJUXJUXMQPQUOUWKVKVLVMUXMPUWL
      PUWKVNVOVBSZUWJUXBUXIUXCUXHUXBHUWIIUXFUWIUGUXGUXADUWLUXFUWIUWTLVPVQVRWOZU
      WJVSHDDUWLUWIVTWAWBUWJUWPUWJUWPVGUWIUWMWCRZWDRZCUAZKMZCQUNZUWQUWJUWPUYBUW
      JUWPNZUXSIJZUYBUYCUXRIJZUXRWEUMZUYDUWJUYEUWPUWJUWRUYEUWSUWIUWMWFWOWGZUYCU
      YEWEUXRKMZUYFUYGUWJUWPUYHUWRUWJUWPUYHOUWSUWMUWIWHUIWIZUXRWJWKUXRWLWKUXSCW
      MSWNUWJUYAUWQCQUWJUXTQJZUYAUWQUWJUYJUYAWPZUWPBIKUCZUYLVGWQRZKMZUYKUYLIJZU
      YNUWJUYJUYOUYAUWJUYJNZUYLTJUYOABCDEFGWRZUYLWSSZWTUYLXASUWJUYJUYAUWPUYNUDZ
      XGUWPUYJUYAUWJUYSUWJUWPUYJUYAUYSUWJUWPUYJUYAUYSXGUYCUYJNZUYAUXTUWKUBZVGUX
      TWDRZWQRZUWIKMZUYSUYTUYAVUBUXRKMZVUDUYCUYEUYHNUXTIJZWEUXTKMZNZUYAVUEOUYJU
      YCUYEUYHUYGUYIXBUYJVUFVUGUXTXCZUXTXDXBZUXRUXTXEXFUYTVUEUWMVUBWQRZUWIKMZVU
      DUYTUWRVUBIJZUWJVULVUEOUWJUWRUWPUYJUWSXHUYJVUMUYCUXTXIZXJUWJUWPUYJXKUWMVU
      BUWIXLXMUWJUYJVULVUDXGZUWPUYPVUCVUKLMZVUOUYPVUAUWMLMZVUPUYPUXDUXEUXIWPZVU
      AUWLJZVUQUWJVURUYJUWJUXDUXEUXIUXLUXPUXQXNWGUWJUWKQXOZUYJVUSUWJUXJVUTUXKQU
      OUWKXPSQUXTUWKXQXRZHDUWLVUAXSWKUYPVUAIJZUWRVUMVUQVUPOUYPUWLIVUAUWJUXDUYJU
      XLWGVVAXTZUWJUWRUYJUWSWGUYJVUMUWJVUNXJZVUAUWMVUBYAXMYBUYPVUCIJZVUKIJZUWJV
      UPVUOXGUYPVVBVUMVVEVVCVVDVUAVUBYCWKUWJUWRVUMVVFUYJUWSVUNUWMVUBYCXFUWJUYJY
      DVVEVVFUWJWPVUPVULVUDVUCVUKUWIYEYFXMYGYHYIYJUWJUYJVUDUYSXGUWPUYPUYMUXTWDR
      ZUWIKMZUYMUYLLMZVUDUYSUYPVVHVVIUYPVVHUYMBJZVVIUYPUYMTJZVVHVVJUYPUYLUYQYKZ
      VVJVVKVVHNUWTUXTWDRZUWIKMZVVHDUYMTBUWTUYMUGVVMVVGUWIKUWTUYMUXTWDUUAYLFUUB
      UUCXRUYPBIULZBPUMZUXGDBUJZHIUNZWPVVJVVIUYPVVOVVPVVRVVOUYPBTIBVVNDTUUDZTFV
      VNDTUUEUUFUUGUUKYMUYPVVSPUMZVVPUYPVVNDTUNZVVTUYPVWAUWTUXTUWIUULRZKMZDTUNZ
      UYPVWBIJZVWDUYJUWJVUFVWEVUIVUFUWJVWEUXTUWIUUHZUUIUUJZVWEVWDVWBUWTKMDTUNDD
      VWBUUMUUNSUYPVVNVWCDTUYPUWTTJZNZUWTIJZUWJVUHVVNVWCOVWHVWJUYPUWTWSXJZUWJUY
      JVWHXKZUYJVUHUWJVWHVUJYNUWTUWIUXTUUOXMZUUPWBVVNDTUUQYOBVVSPFUVBYOUYPVWEUW
      TVWBLMZDBUJZVVRVWGUYPVWNDBUWTBJUYPVWHVVNNVWNVVNDBTFUURUYPVWHVVNVWNVWIVVNV
      WCVWNVWMVWIVWJVWEVWCVWNXGVWKVWIVUFUWJVWEUYJVUFUWJVWHVUIYNVWLVWFWKUWTVWBUU
      SWKYJUUTUVAUVCVVQVWOHVWBIUXFVWBUGUXGVWNDBUXFVWBUWTLVPVQVRWKXNHDBUYMXSXRUV
      DWNUYPVVGVUCUWIKUYPVVGUYLUXTWDRZVUBWQRZVUCUYPUYLYTJVGYTJZUXTYTJZUXTWEUMZN
      ZVVGVWQUGUYPUYLUYRUVEVWRUYPUVFYMUYJVXAUWJUYJVWSVWTUXTUVGUXTUVHXBXJUYLVGUX
      TUVIXMUYPVUAVWPVUBWQUWJUYJVUAUXTCQVWPUVJZUBZVWPUWJUXTUWKVXBUWJVXBYPJUWKVX
      BUGCQVWPUTUVMAIVXBYPEGYQYRUVNUYJVWPYPJVXCVWPUGUYLUXTWDUVKCQVWPYPVXBVXBUVO
      YQYRUVLUVPUVQYLUYPUYMIJZUYOVVIUYSOUYPVVKVXDVVLUYMWSSUYRUYMUYLUVRWKUVSYHYS
      UVTUWAUWBUWCUWDUWEUWFYSUWGUWH $.
      $( [12-May-2013] $)

    $( One half of ~ rpnnen , where we show an injection from the real numbers
       to sequences of rational numbers.  Specifically, we map a real number
       ` x ` to the sequence ` ( F `` x ) : NN --> QQ ` such that
       ` ( ( F `` x ) `` k ) ` is the largest rational number with denominator
       ` k ` that is strictly less than ` x ` .  In this manner, we get a
       monotonically increasing sequence that converges to ` x ` , and since
       each sequence converges to a unique real number, this mapping from reals
       to sequences of rational numbers is injective.  (Contributed by Mario
       Carneiro, 13-May-2013.) $)
    rpnnen1 $p |- RR ~<_ ( QQ ^m NN ) $=
      ( vy cq cn cmap cvv wcel cr cv cfv wceq crn clt csup cdom wbr rpnnen1lem1
      co ovex wa rneq supeq1d rpnnen1lem5 fveq2 rneqd eqeq12d vtoclga eqeqan12d
      weq id syl5ib impbid1 dom3 ax-mp ) IJKUDZLMNVAUAUBIJKUEAHNVAAOZEPZHOZEPZL
      ABCDEFGUCVBNMZVDNMZUFZVCVEQZAHUOZVIVCRZNSTZVERZNSTZQVHVJVINVKVMSVCVEUGUHV
      FVGVLVBVNVDABCDEFGUIZVLVBQVNVDQAVDNVJVLVNVBVDVJNVKVMSVJVCVEVBVDEUJZUKUHVJ
      UPULVOUMUNUQVPURUSUT $.
      $( [16-Jun-2013] $) $( [12-May-2013] $)
  $}

  ${
    $d j k m n x y $.
    $( The set of real numbers exists. $)
    reex $p |- RR e. _V $=
      ( vx vm vk vn vy vj cr cn co cdom wbr cdiv clt crab csup cmpt wceq breq1d
      cv cz cq cmap wcel oveq1 cbvrabv oveq2 rabbidv supeq1d id oveq12d cbvmptv
      cvv breq2 oveq1d mpteq2dv syl5eq rpnnen1 reldom brrelexi ax-mp ) GUAHUBIZ
      JKGULUCABSZCSZLIZASZMKZBTNZCDEGFHVBFSZLIZESZMKZBTNZGMOZVHLIZPZPVFDSZVCLIZ
      VEMKBDTVBVPQVDVQVEMVBVPVCLUDRUEEAGVOCHVGGMOZVCLIZPZVJVEQZVOCHVDVJMKZBTNZG
      MOZVCLIZPVTFCHVNWEVHVCQZVMWDVHVCLWFGVLWCMWFVKWBBTWFVIVDVJMVHVCVBLUFRUGUHW
      FUIUJUKWACHWEVSWAWDVRVCLWAGWCVGMWAWBVFBTVJVEVDMUMUGUHUNUOUPUKUQGVAJURUSUT
      $.
      $( [23-Aug-2014] $) $( [30-Jul-2004] $)
  $}

  ${
    $d F u v w z $.  $d x y u v w z $.
    cnref1o.1 $e |- F = ( x e. RR , y e. RR |-> ( x + ( _i x. y ) ) ) $.
    $( There is a natural one-to-one mapping from ` ( RR X. RR ) ` to ` CC ` ,
       where we map ` <. x , y >. ` to ` ( x + ( _i x. y ) ) ` .  In our
       construction of the complex numbers, this is in fact our _definition_ of
       ` CC ` (see ~ df-c ), but in the axiomatic treatment we can only show
       that there is the expected mapping between these two sets.  (Contributed
       by Mario Carneiro, 16-Jun-2013.) $)
    cnref1o $p |- F : ( RR X. RR ) -1-1-onto-> CC $=
      ( vz vw vu vv cr cc cv cfv wceq wcel ci cmul co caddc c2nd wa cxp wf1 wfo
      wf1o df-f1o wf wi wral dff13 wfn ffnfv ovex fnmpt2i c1st cop fveq2d df-ov
      1st2nd2 syl6eqr xp1st xp2nd oveq1 oveq2 oveq2d ovmpt2 syl2anc eqtrd recnd
      ax-icn ax-mulcl sylancr ax-addcl eqeltrd mpbir2an wb jca cru syl2an fveq2
      rgen oveq12d eqeq12d vtoclga eqeqan12d syl6bb 3bitr4d biimpd rgen2a dffo3
      fvex opth wrex ax-cnre eqeq2d 2rexbiia sylibr rexxp ) IIUAZJCUDWRJCUBZWRJ
      CUCZWRJCUEWSWRJCUFZEKZCLZFKZCLZMZXBXDMZUGZFWRUHEWRUHEFWRJCUIXACWRUJXCJNZE
      WRUHEWRJCUKABIIAKZOBKZPQZRQZCDXJXLRULUMXIEWRXBWRNZXCXBUNLZOXBSLZPQZRQZJXN
      XCXOXPCQZXRXNXCXOXPUOZCLXSXNXBXTCXBIIURZUPXOXPCUQUSXNXOINZXPINZXSXRMXBIIU
      TZXBIIVAZABXOXPIIXMXRCXOXLRQXJXOXLRVBXKXPMXLXQXORXKXPOPVCVDDXOXQRULVEVFVG
      ZXNXOJNXQJNZXRJNXNXOYDVHXNOJNXPJNYGVIXNXPYEVHOXPVJVKXOXQVLVFVMVTVNZXHEFWR
      XNXDWRNZTZXFXGYJXRXDUNLZOXDSLZPQZRQZMZXOYKMXPYLMTZXFXGXNYBYCTYKINZYLINZTY
      OYPVOYIXNYBYCYDYEVPYIYQYRXDIIUTXDIIVAVPXOXPYKYLVQVRXNYIXCXRXEYNYFXCXRMXEY
      NMEXDWRXGXCXEXRYNXBXDCVSXGXOYKXQYMRXBXDUNVSXGXPYLOPXBXDSVSVDWAWBYFWCWDYJX
      GXTYKYLUOZMYPXNYIXBXTXDYSYAXDIIURWDXOXPYKYLXBUNWJXBSWJXDSWJWKWEWFWGWHVNWT
      XAXDXCMZEWRWLZFJUHEFWRJCWIYHUUAFJXDJNZXDGKZHKZCQZMZHIWLGIWLZUUAUUBXDUUCOU
      UDPQZRQZMZHIWLGIWLUUGGHXDWMUUFUUJGHIIUUCINUUDINTUUEUUIXDABUUCUUDIIXMUUICU
      UCXLRQXJUUCXLRVBXKUUDMXLUUHUUCRXKUUDOPVCVDDUUCUUHRULVEWNWOWPYTUUFEGHIIXBU
      UCUUDUOZMZXCUUEXDUULXCUUKCLUUEXBUUKCVSUUCUUDCUQUSWNWQWPVTVNVN $.
      $( [17-Feb-2014] $) $( [16-Jun-2013] $)
  $}

  ${
    $d x y $.
    $( The set of complex numbers exists.  (Revised by Mario Carneiro,
       16-Jun-2013.) $)
    cnex $p |- CC e. _V $=
      ( vx vy cr cxp cvv wcel cc cv ci cmul caddc cmpt2 wfo reex xpex wf1o eqid
      co cnref1o f1ofo ax-mp fornex mp2 ) CCDZEFUDGABCCAHIBHJRKRLZMZGEFCCNNOUDG
      UEPUFABUEUEQSUDGUETUAUDGEUEUBUC $.
      $( [16-Jun-2013] $) $( [30-Jul-2004] $)
  $}

  $( The set of extended reals exists. $)
  xrex $p |- RR* e. _V $=
    ( cxr cr cpnf cmnf cpr cun cvv df-xr reex prex unex eqeltri ) ABCDEZFGHBMIC
    DJKL $.
    $( [24-Dec-2006] $)

  $( The addition operation is a set. $)
  addex $p |- + e. _V $=
    ( cc cxp caddc wf cvv wcel ax-addopr cnex xpex fex mp2an ) AABZACDLEFCEFGAA
    HHILAECJK $.
    $( [19-Oct-2004] $)

  $( The multiplication operation is a set. $)
  mulex $p |- x. e. _V $=
    ( cc cxp cmul wf cvv wcel ax-mulopr cnex xpex fex mp2an ) AABZACDLEFCEFGAAH
    HILAECJK $.
    $( [19-Oct-2004] $)

