$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intermediate value theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d c x y z B $.  $d c x y z D $.  $d c x y z F $.  $d c x y z ph $.
    $d c x y A $.  $d x y z C $.  $d x y z S $.  $d c x y z U $.
    ivth.1 $e |- ( ph -> A e. RR ) $.
    ivth.2 $e |- ( ph -> B e. RR ) $.
    ivth.3 $e |- ( ph -> U e. RR ) $.
    ivth.4 $e |- ( ph -> A < B ) $.
    ivth.5 $e |- ( ph -> ( A [,] B ) C_ D ) $.
    ivth.6 $e |- ( ph -> D C_ CC ) $.
    ivth.7 $e |- ( ph -> F e. ( D -cn-> CC ) ) $.
    ivth.8 $e |- ( ( ph /\ x e. ( A [,] B ) ) -> ( F ` x ) e. RR ) $.
    ${
      ivth.9 $e |- ( ph -> ( ( F ` A ) < U /\ U < ( F ` B ) ) ) $.
      ${
        ivth.10 $e |- S = { x e. ( A [,] B ) | ( F ` x ) <_ U } $.
        $( Lemma for ~ ivth .  The set ` S ` of all ` x ` values with
           ` ( F `` x ) ` less than ` U ` is lower bounded by ` A ` and upper
           bounded by ` B ` . $)
        ivthlem1 $p |- ( ph -> ( A e. S /\ A. z e. S z <_ B ) ) $=
          ( wcel cv cle wbr wral cicc co cfv cr clt wi ltle syl2anc mpd syl3anc
          lbicc2 simpld ralrimiva wceq fveq2 eleq1d rcla4v sylc breq1d sylanbrc
          elrab2 crab ssrab2 eqsstri sseli cxr rexr syl iccleub 3expia ralrimiv
          syl5 jca ) ADGTZCUAZEUBUCZCGUDADDEUEUFZTZDIUGZHUBUCZVRADUHTZEUHTZDEUB
          UCZWBJKADEUIUCZWGMAWEWFWHWGUJJKDEUKULUMDEUOUNZAWCHUIUCZWDAWJHEIUGUIUC
          RUPAWCUHTZHUHTWJWDUJAWBBUAZIUGZUHTZBWAUDWKWIAWNBWAQUQWNWKBDWAWLDURZWM
          WCUHWLDIUSZUTVAVBLWCHUKULUMWMHUBUCZWDBDWAGWOWMWCHUBWPVCSVEVDAVTCGVSGT
          VSWATZAVTGWAVSGWQBWAVFWASWQBWAVGVHVIADVJTZEVJTZWRVTUJAWEWSJDVKVLAWFWT
          KEVKVLWSWTWRVTDEVSVMVNULVPVOVQ $.
          $( [17-Jun-2014] $)

        ivth.11 $e |- C = sup ( S , RR , < ) $.
        $( Lemma for ~ ivth .  Show that the supremum of ` S ` cannot be less
           than ` U ` .  If it was, continuity of ` F ` implies that there are
           points just above the supremum that are also less than ` U ` , a
           contradiction. $)
        ivthlem2 $p |- ( ph -> -. ( F ` C ) < U ) $=
          ( vy vz cfv clt wbr wn wa cv cmin co cabs wi wral crp wrex ccncf wcel
          cc wss adantr ssid a1i cicc cr cle w3a wb elicc2 syl2anc csup c0 crab
          wne ssrab2 eqsstri iccssre syl5ss ivthlem1 simpld ne0i syl wceq breq2
          simprd ralbidv rcla4ev 3jca suprcl syl5eqel suprub syl6breqr suprleub
          syl22anc syl5eqbr mpbir3and sseldd ralrimiva fveq2 eleq1d rcla4v sylc
          difrp biimpa cncfi syl32anc ssralv ad2antrr caddc cif rphalfcl adantl
          cdiv rpre readdcl ifcl ltle mpd ubicc2 syl3anc lttr mpan2d imp breq2d
          c2 ltnr notbid syl5ibrcom necon2ad jctild ltlen sylibrd ltaddrp letrd
          ifboth breq1d recnd ex syld syl5 rexlimdva min1 abssubge0 min2 ltadd2
          rphalflt mpbid lelttrd ltsubadd2 mpbird eqbrtrd oveq1 fveq2d syl12anc
          anbi12d r19.29 pm3.45 simprr simprl simplll resubcl absdiflt ad2antrl
          pncan3 elrab2 3imtr4d lenlt sylibd adantld sylbid pm2.21d com23 imp3a
          baib mt2d expr pm2.01d ) AEIUCZHUDUEZAUVRUVRUFZAUVRUGZUAUHZEUIUJZUKUC
          ZUBUHZUDUEZUWAIUCZUVQUIUJUKUCHUVQUIUJZUDUEZULZUAFUMZUBUNUOZUVSUVTFURU
          SZURURUSZIFURUPUJUQZEFUQZUWGUNUQZUWKAUWLUVROUTUWMUVTURVAVBAUWNUVRPUTA
          UWOUVRACDVCUJZFENAEUWQUQZEVDUQZCEVEUEZEDVEUEZACVDUQZDVDUQZUWRUWSUWTUX
          AVFVGJKCDEVHVIAEGVDUDVJZVDTAGVDUSZGVKVMZUWDBUHZVEUEZUBGUMZBVDUOZVFZUX
          DVDUQAUXEUXFUXJAGUWQVDGUXGIUCZHVEUEZBUWQVLUWQSUXMBUWQVNVOAUXBUXCUWQVD
          USZJKCDVPVIZVQZACGUQZUXFAUXQUWDDVEUEZUBGUMZABUBCDFGHIJKLMNOPQRSVRZVSZ
          GCVTWAZAUXCUXSUXJKAUXQUXSUXTWDZUXIUXSBDVDUXGDWBZUXHUXRUBGUXGDUWDVEWCW
          EWFVIWGZBUBGWHWAWIZACUXDEVEAUXKUXQCUXDVEUEUYEUYABUBGCWJVITWKZAEUXDDVE
          TAUXEUXFUXCUXSUXDDVEUEUXPUYBKUYCUBGDWLWMWNZWOZWPUTAUVRUWPAUVQVDUQZHVD
          UQZUVRUWPVGAUWRUXLVDUQZBUWQUMZUYJUYIAUYLBUWQQWQZUYLUYJBEUWQUXGEWBUXLU
          VQVDUXGEIWRWSWTXAZLUVQHXBVIXCUBUAFUREUWGIXDXEUVTUWJUVSUBUNUVTUWDUNUQZ
          UGZUWJUWIUAUWQUMZUVSAUWJUYRULZUVRUYPAUWQFUSUYSNUWIUAUWQFXFWAXGUYQUYRU
          WEEUWAUDUEZUGZUAUWQUOZUVSUYQDEUWDYDXLUJZXHUJZVEUEZDVUDXIZUWQUQZVUFEUI
          UJZUKUCZUWDUDUEZEVUFUDUEZVUBUYQVUGVUFVDUQZCVUFVEUEZVUFDVEUEZAVUGVULVU
          MVUNVFVGZUVRUYPAUXBUXCVUOJKCDVUFVHVIXGUYQUXCVUDVDUQZVULAUXCUVRUYPKXGZ
          UYQUWSVUCVDUQZVUPAUWSUVRUYPUYFXGZUYQVUCUNUQZVURUYPVUTUVTUWDXJXKZVUCXM
          WAZEVUCXNVIZVUEDVUDVDXOVIZUYQCEVUFAUXBUVRUYPJXGVUSVVDAUWTUVRUYPUYGXGU
          YQVUKEVUFVEUEZUYQEDUDUEZEVUDUDUEZVUKUYQUVQDIUCZUDUEZVVFUVTVVIUYPAUVRV
          VIAUVRHVVHUDUEZVVIACIUCHUDUEVVJRWDAUYJUYKVVHVDUQZUVRVVJUGVVIULUYOLADU
          WQUQZUYMVVKAUXBUXCCDVEUEZVVLJKACDUDUEZVVMMAUXBUXCVVNVVMULJKCDXPVIXQCD
          XRXSUYNUYLVVKBDUWQUYDUXLVVHVDUXGDIWRWSWTXAUVQHVVHXTXSYAYBUTAVVIVVFULU
          VRUYPAVVIUXADEVMZUGZVVFAVVIVVOUXAAVVIDEAVVIUFDEWBZUVQUVQUDUEZUFZAUYJV
          VSUYOUVQYEWAVVQVVIVVRVVQVVHUVQUVQUDDEIWRYCYFYGYHUYHYIAUWSUXCVVFVVPVGU
          YFKEDYJVIYKXGXQUYQUWSVUTVVGVUSVVAEVUCYLVIVUEVVFVVGVUKDVUDDVUFEUDWCVUD
          VUFEUDWCYNVIZUYQUWSVULVUKVVEULVUSVVDEVUFXPVIXQZYMUYQUXCVUPVUNVUQVVCDV
          UDUUAVIWOUYQVUIVUHUWDUDUYQUWSVULVVEVUIVUHWBVUSVVDVWAEVUFUUBXSUYQVUHUW
          DUDUEZVUFEUWDXHUJZUDUEZUYQVUFVUDVWCVVDVVCUYQUWSUWDVDUQZVWCVDUQVUSUYPV
          WEUVTUWDXMXKZEUWDXNVIUYQUXCVUPVUFVUDVEUEVUQVVCDVUDUUCVIUYQVUCUWDUDUEZ
          VUDVWCUDUEZUYPVWGUVTUWDUUEXKUYQVURVWEUWSVWGVWHVGVVBVWFVUSVUCUWDEUUDXS
          UUFUUGUYQVULUWSVWEVWBVWDVGVVDVUSVWFVUFEUWDUUHXSUUIUUJVVTVUAVUJVUKUGUA
          VUFUWQUWAVUFWBZUWEVUJUYTVUKVWIUWCVUIUWDUDVWIUWBVUHUKUWAVUFEUIUUKUULYO
          UWAVUFEUDWCUUNWFUUMUYRVUBUGUWIVUAUGZUAUWQUOUYQUVSUWIVUAUAUWQUUOUYQVWJ
          UVSUAUWQVWJUWHUYTUGZUYQUWAUWQUQZUGZUVSUWIVUAVWKUWEUWHUYTUUPYBVWMUWHUY
          TUVSVWMUYTUWHUVSUYQVWLUYTUWHUVSULUYQVWLUYTUGZUGZUWHUVSVWOUWHUYTUYQVWL
          UYTUUQVWOUWHUVQUWGUIUJUWFUDUEZUWFUVQUWGXHUJZUDUEZUGZUYTUFZVWOUWFVDUQZ
          UYJUWGVDUQZUWHVWSVGVWOVWLUYMVXAUYQVWLUYTUURZVWOAUYMAUVRUYPVWNUUSZUYNW
          AUYLVXABUWAUWQUXGUWAWBZUXLUWFVDUXGUWAIWRZWSWTXAZVWOAUYJVXDUYOWAZVWOUY
          KUYJVXBVWOAUYKVXDLWAZVXHHUVQUUTVIUWFUVQUWGUVAXSVWOVWRVWTVWPVWOVWRUWAG
          UQZVWTVWOUWFHUDUEZUWFHVEUEZVWRVXJVWOVXAUYKVXKVXLULVXGVXIUWFHXPVIVWOVW
          QHUWFUDVWOUVQURUQHURUQVWQHWBVWOUVQVXHYPVWOHVXIYPUVQHUVCVIYCVWLVXJVXLV
          GUYQUYTVXJVWLVXLUXMVXLBUWAUWQGVXEUXLUWFHVEVXFYOSUVDUVMUVBUVEVWOVXJUWA
          EVEUEZVWTVWOAVXJVXMULZVXDAUXKVXNUYEUXKVXJVXMUXKVXJUGUWAUXDEVEBUBGUWAW
          JTWKYQWAWAVWOUWAVDUQUWSVXMVWTVGVWOUWQVDUWAVWOAUXNVXDUXOWAVXCWPVWOAUWS
          VXDUYFWAUWAEUVFVIUVGYRUVHUVIUVNUVJUVOUVKUVLYSYTYSYAYRYTXQYQUVP $.
          $( [17-Jun-2014] $)

        $( Lemma for ~ ivth , the intermediate value theorem.  Show that
           ` ( F `` C ) ` cannot be greater than ` U ` , and so establish the
           existence of a root of the function. $)
        ivthlem3 $p |- ( ph -> ( C e. ( A (,) B ) /\ ( F ` C ) = U ) ) $=
          ( vz vy cioo co wcel cfv wceq clt wbr cxr w3a rexr syl elioo2 syl2anc
          cr wb csup wss c0 wne cv wral wrex cicc ssrab2 eqsstri iccssre syl5ss
          cle crab ivthlem1 simpld simprd breq2 ralbidv rcla4ev suprcl syl5eqel
          ne0i 3jca wn wa elicc2 syl6breqr suprleub syl22anc syl5eqbr mpbir3and
          suprub ralrimiva fveq2 eleq1d rcla4v sylc lttri3 ivthlem2 cmin wi crp
          cabs cc ccncf adantr ssid sseldd difrp biimpa cncfi syl32anc ad2antrr
          ssralv rpre adantl resubcl ltsubrp sylancom syl6breq suprlub syl12anc
          sseli ad2antrl simplll syl3anc mpbid eqbrtrd ex ad2antll recnd breq1d
          a1i mpd sylbid syl5 rexlimdva notbid necon2ad jctild ltlen sylibrd
          simprl abssuble0 simprr ltsub23 jca32 reximdv2 r19.29 pm3.45 absdiflt
          imp caddc nncan elrab2 simprbi lenlt pm2.21d adantrd expr com23 imp3a
          mpan2d syld pm2.01d mpbir2and breqtrrd ltnr syl5ibcom syl5ibrcom jca
          breq2d ) AECDUCUDUEZEIUFZHUGZAUVKEUPUEZCEUHUIZEDUHUIZACUJUEZDUJUEZUVK
          UVNUVOUVPUKUQACUPUEZUVQJCULUMADUPUEZUVRKDULUMCDEUNUOAEGUPUHURZUPTAGUP
          USZGUTVAZUAVBZBVBZVJUIZUAGVCZBUPVDZUKZUWAUPUEAUWBUWCUWHAGCDVEUDZUPGUW
          EIUFZHVJUIZBUWJVKUWJSUWLBUWJVFVGZAUVSUVTUWJUPUSZJKCDVHUOZVIZACGUEZUWC
          AUWQUWDDVJUIZUAGVCZABUACDFGHIJKLMNOPQRSVLZVMZGCVTUMZAUVTUWSUWHKAUWQUW
          SUWTVNZUWGUWSBDUPUWEDUGUWFUWRUAGUWEDUWDVJVOVPVQUOWAZBUAGVRUMVSZACIUFZ
          UVLUHUIZUVOAUXFHUVLUHAUXFHUHUIZHDIUFZUHUIZRVMAUVMUVLHUHUIWBZHUVLUHUIZ
          WBZAUVLUPUEZHUPUEZUVMUXKUXMWCUQAEUWJUEZUWKUPUEZBUWJVCZUXNAUXPUVNCEVJU
          IZEDVJUIZAUVSUVTUXPUVNUXSUXTUKUQJKCDEWDUOUXEACUWAEVJAUWIUWQCUWAVJUIUX
          DUXABUAGCWJUOTWEZAEUWADVJTAUWBUWCUVTUWSUWADVJUIUWPUXBKUXCUAGDWFWGWHZW
          IZAUXQBUWJQWKZUXQUXNBEUWJUWEEUGUWKUVLUPUWEEIWLWMWNWOZLUVLHWPUOABCDEFG
          HIJKLMNOPQRSTWQAUXLAUXLUXMAUXLWCZUBVBZEWRUDXAUFZUWDUHUIZUYGIUFZUVLWRU
          DXAUFUVLHWRUDZUHUIZWSZUBFVCZUAWTVDZUXMUYFFXBUSZXBXBUSZIFXBXCUDUEZEFUE
          ZUYKWTUEZUYOAUYPUXLOXDUYQUYFXBXEYKAUYRUXLPXDAUYSUXLAUWJFENUYCXFXDAUXL
          UYTAUXOUXNUXLUYTUQLUYEHUVLXGUOXHUAUBFXBEUYKIXIXJUYFUYNUXMUAWTUYFUWDWT
          UEZWCZUYNUYMUBUWJVCZUXMAUYNVUCWSZUXLVUAAUWJFUSVUDNUYMUBUWJFXLUMXKVUBV
          UCUYIUYGGUEZWCZUBUWJVDZUXMVUBEUWDWRUDZUYGUHUIZUBGVDZVUGVUBUWIVUHUPUEZ
          VUHUWAUHUIVUJAUWIUXLVUAUXDXKVUBUVNUWDUPUEZVUKAUVNUXLVUAUXEXKZVUAVULUY
          FUWDXMXNZEUWDXOUOVUBVUHEUWAUHUYFVUAUVNVUHEUHUIVUMEUWDXPXQTXRBUAUBGVUH
          XSXTVUBVUIVUFUBGUWJVUBVUEVUIWCZUYGUWJUEZVUFWCVUBVUOWCZVUPUYIVUEVUEVUP
          VUBVUIGUWJUYGUWMYAZYBZVUQUYHEUYGWRUDZUWDUHVUQUYGUPUEZUVNUYGEVJUIUYHVU
          TUGVUQUWJUPUYGVUQAUWNAUXLVUAVUOYCZUWOUMVUSXFZVUQAUVNVVBUXEUMZVUQUYGUW
          AEVJVUQUWIVUEUYGUWAVJUIVUQAUWIVVBUXDUMVUBVUEVUIUUAZBUAGUYGWJUOTWEUYGE
          UUBYDVUQVUIVUTUWDUHUIZVUBVUEVUIUUCVUQUVNVULVVAVUIVVFUQVVDVUBVULVUOVUN
          XDVVCEUWDUYGUUDYDYEYFVVEUUEYGUUFYLVUCVUGWCUYMVUFWCZUBUWJVDVUBUXMUYMVU
          FUBUWJUUGVUBVVGUXMUBUWJVVGUYLVUEWCZVUBVUPWCUXMUYMVUFVVHUYIUYLVUEUUHUU
          JVUBVVHUXMWSVUPVUBUYLVUEUXMVUBVUEUYLUXMUYFVUAVUEUYLUXMWSUYFVUAVUEWCZW
          CZUYLUVLUYKWRUDZUYJUHUIZUYJUVLUYKUUKUDUHUIZWCZUXMVVJUYJUPUEZUXNUYKUPU
          EZUYLVVNUQVVJVUPUXRVVOVUEVUPUYFVUAVURYHAUXRUXLVVIUYDXKUXQVVOBUYGUWJUW
          EUYGUGZUWKUYJUPUWEUYGIWLZWMWNWOZAUXNUXLVVIUYEXKZVVJUXNUXOVVPVVTAUXOUX
          LVVILXKZUVLHXOUOUYJUVLUYKUUIYDVVJVVLUXMVVMVVJVVLHUYJUHUIZUXMVVJVVKHUY
          JUHVVJUVLXBUEHXBUEVVKHUGVVJUVLVVTYIVVJHVWAYIUVLHUULUOYJVVJVWBUXMVVJUY
          JHVJUIZVWBWBZVUEVWCUYFVUAVUEVUPVWCUWLVWCBUYGUWJGVVQUWKUYJHVJVVRYJSUUM
          UUNYHVVJVVOUXOVWCVWDUQVVSVWAUYJHUUOUOYEUUPYMUUQYMUURUUSUUTXDYNYOYNUVA
          UVBYOYLYGUVCUVDZUVEAUXGUXSECVAZWCZUVOAUXGVWFUXSAUXGECAUVLUVLUHUIZWBZE
          CUGZUXGWBAUXNVWIUYEUVLUVFUMZVWJVWHUXGVWJUVLUXFUVLUHECIWLYJYPUVGYQUYAY
          RAUVSUVNUVOVWGUQJUXECEYSUOYTYLAUVLUXIUHUIZUVPAUVLHUXIUHVWEAUXHUXJRVNY
          FAVWLUXTDEVAZWCZUVPAVWLVWMUXTAVWLDEAVWLWBDEUGZVWIVWKVWOVWLVWHVWOUXIUV
          LUVLUHDEIWLUVJYPUVHYQUYBYRAUVNUVTUVPVWNUQUXEKEDYSUOYTYLWIVWEUVI $.
          $( [17-Jun-2014] $) $( [30-Apr-2014] $)
      $}

      $( The intermediate value theorem, increasing case.  (Contributed by Paul
         Chapman, 22-Jan-2008.)  (The proof was shortened by Mario Carneiro,
         30-Apr-2014.) $)
      ivth $p |- ( ph -> E. c e. ( A (,) B ) ( F ` c ) = U ) $=
        ( vy cfv wceq cv cle wbr cicc co crab cr clt csup cioo wcel wrex breq1d
        wa fveq2 cbvrabv eqid ivthlem3 eqeq1d rcla4ev syl ) ARUAZGSZFUBUCZRCDUD
        UEZUFZUGUHUIZCDUJUEZUKVGGSZFTZUNHUAZGSZFTZHVHULABCDVGEVFFGIJKLMNOPQVDBU
        AZGSZFUBUCRBVEVBVNTVCVOFUBVBVNGUOUMUPVGUQURVMVJHVGVHVKVGTVLVIFVKVGGUOUS
        UTVA $.
        $( [30-Apr-2014] $) $( [22-Jan-2008] $)
    $}

    ${
      ivth2.9 $e |- ( ph -> ( ( F ` B ) < U /\ U < ( F ` A ) ) ) $.
      $( The intermediate value theorem, decreasing case.  (Contributed by Paul
         Chapman, 22-Jan-2008.)  (Revised by Mario Carneiro, 30-Apr-2014.) $)
      ivth2 $p |- ( ph -> E. c e. ( A (,) B ) ( F ` c ) = U ) $=
        ( wceq cr wcel vy cv cfv cneg cmpt cioo co wrex renegcl syl cc wss eqid
        ccncf negfcncf syl2anc cicc wa sseld imp fveq2 negeqd negex eqeltrd clt
        fvmpt wbr cle wi ltle mpd lbicc2 syl3anc sseldd simprd ralrimiva eleq1d
        wb wral rcla4v sylc ltneg mpbid eqbrtrd simpld ubicc2 breqtrrd jca ivth
        ioossicc syl5ss eqeq1d ssid a1i cncff ffvelrn sylan syldan recnd adantr
        wf neg11 bitrd rexbidva ) AHUBZUAEUAUBZGUCZUDZUEZUCZFUDZRZHCDUFUGZUHXEG
        UCZFRZHXMUHABCDEXKXIHIJAFSTZXKSTKFUIUJLMNAEUKULZGEUKUNUGZTZXIXRTNOUAEGX
        IXIUMZUOUPABUBZCDUQUGZTZURZYAXIUCZYAGUCZUDZSYDYAETZYEYGRAYCYHAYBEYAMUSU
        TUAYAXHYGEXIXFYARXGYFXFYAGVAVBXTYFVCVFUJYDYFSTZYGSTPYFUIUJVDACXIUCZXKVE
        VGXKDXIUCZVEVGAYJCGUCZUDZXKVEACETYJYMRAYBECMACSTZDSTZCDVHVGZCYBTZIJACDV
        EVGZYPLAYNYOYRYPVIIJCDVJUPVKZCDVLVMZVNUACXHYMEXIXFCRXGYLXFCGVAVBXTYLVCV
        FUJAFYLVEVGZYMXKVEVGZADGUCZFVEVGZUUAQVOAXPYLSTZUUAUUBVRKAYQYIBYBVSZUUEY
        TAYIBYBPVPZYIUUEBCYBYACRYFYLSYACGVAVQVTWAFYLWBUPWCWDAXKUUCUDZYKVEAUUDXK
        UUHVEVGZAUUDUUAQWEAUUCSTZXPUUDUUIVRADYBTZUUFUUJAYNYOYPUUKIJYSCDWFVMZUUG
        YIUUJBDYBYADRYFUUCSYADGVAVQVTWAKUUCFWBUPWCADETYKUUHRAYBEDMUULVNUADXHUUH
        EXIXFDRXGUUCXFDGVAVBXTUUCVCVFUJWGWHWIAXLXOHXMAXEXMTZURZXLXNUDZXKRZXOUUN
        XJUUOXKUUNXEETZXJUUORAUUMUUQAXMEXEAXMYBECDWJMWKUSUTZUAXEXHUUOEXIXFXERXG
        XNXFXEGVAVBXTXNVCVFUJWLUUNXNUKTZFUKTZUUPXOVRAUUMUUQUUSUURAEUKGXAZUUQUUS
        AXQUKUKULZXSUVANUVBAUKWMWNOEUKGWOVMEUKXEGWPWQWRAUUTUUMAFKWSWTXNFXBUPXCX
        DWC $.
        $( [30-Apr-2014] $) $( [22-Jan-2008] $)
    $}

    ivthle2.9 $e |- ( ph -> ( ( F ` B ) <_ U /\ U <_ ( F ` A ) ) ) $.
    $( The intermediate value theorem with weak inequality, decreasing case.
       (Contributed by Mario Carneiro, 12-May-2014.) $)
    ivthle2 $p |- ( ph -> E. c e. ( A [,] B ) ( F ` c ) = U ) $=
      ( wceq cr wcel cfv clt wbr wo cv cicc co wrex simpld wb wral ltle syl2anc
      cle wi mpd ubicc2 syl3anc ralrimiva fveq2 eleq1d rcla4v sylc leloe simprd
      mpbid wa lbicc2 adantr cioo wss ioossicc ccncf adantlr simpr ivth2 ssrexv
      cc mpsyl anassrs eqcom eqeq2d syl5bb rcla4ev sylan jaodan mpdan eqeq1d )
      ADGUAZFUBUCZWIFRZUDZHUEZGUAZFRZHCDUFUGZUHZAWIFUNUCZWLAWRFCGUAZUNUCZQUIAWI
      STZFSTZWRWLUJADWPTZBUEZGUAZSTZBWPUKZXAACSTZDSTZCDUNUCZXCIJACDUBUCZXJLAXHX
      IXKXJUOIJCDULUMUPZCDUQURZAXFBWPPUSZXFXABDWPXDDRXEWISXDDGUTVAVBVCKWIFVDUMV
      FAWJWQWKAWJVGZFWSUBUCZFWSRZUDZWQAXRWJAWTXRAWRWTQVEAXBWSSTZWTXRUJKACWPTZXG
      XSAXHXIXJXTIJXLCDVHURZXNXFXSBCWPXDCRXEWSSXDCGUTVAVBVCFWSVDUMVFVIXOXPWQXQA
      WJXPWQCDVJUGZWPVKAWJXPVGZVGZWOHYBUHWQCDVLYDBCDEFGHAXHYCIVIAXIYCJVIAXBYCKV
      IAXKYCLVIAWPEVKYCMVIAEVRVKYCNVIAGEVRVMUGTYCOVIAXDWPTXFYCPVNAYCVOVPWOHYBWP
      VQVSVTAXQWQWJAXTXQWQYAWOXQHCWPWOFWNRWMCRZXQWNFWAYEWNWSFWMCGUTWBWCWDWEVNWF
      WGAXCWKWQXMWOWKHDWPWMDRWNWIFWMDGUTWHWDWEWFWG $.
      $( [30-Apr-2014] $) $( [22-Jan-2008] $)
  $}


