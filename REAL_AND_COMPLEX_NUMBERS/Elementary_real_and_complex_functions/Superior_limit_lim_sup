$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Superior limit (lim sup)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c limsup $.

  $( Extend class notation to include the limsup function. $)
  clsp $a class limsup $.

  ${
    $d k x y F $.
$(
    df-limsupOLD $a |- limsup = { <. x , y >. | y = sup ( { z |
                  E. k e. ZZ ( ran ( x |` ( ZZ>= ` k ) ) C_ RR* /\
        z = sup ( ran ( x |` ( ZZ>= ` k ) ) , RR* , < ) ) } , RR* , `' < ) } $.

    df-limsupOLD2 $a |- limsup = { <. x , y >. | y = sup ( { z |
   E. k e. ZZ z = sup ( ( x " ( ZZ>= ` k ) ) , RR* , < ) } , RR* , `' < ) } $.
$)

    $( Define the superior limit of an infinite sequence of extended real
       numbers.  Definition 12-4.1 of [Gleason] p. 175.  See ~ limsupval for
       its value. $)
    df-limsup $a |- limsup = ( x e. _V |-> sup ( { y | E. k e. ZZ
   y = sup ( ( ( x " ( ZZ>= ` k ) ) i^i RR* ) , RR* , < ) } , RR* , `' < ) ) $.

    $( The superior limit of an infinite sequence ` F ` of extended real
       numbers, which is the infimum (indicated by ` ``' < ` ) of the set of
       suprema of all upper infinite subsequences of ` F ` .  Definition 12-4.1
       of [Gleason] p. 175. $)
    limsupval $p |- ( F e. A -> ( limsup ` F ) = sup ( { x | E. k e. ZZ x =
       sup ( ( ( F " ( ZZ>= ` k ) ) i^i RR* ) , RR* , < ) } , RR* , `' < ) ) $=
      ( vy wcel cvv clsp cfv cv cima cxr cin clt csup wceq cz wrex cab supeq1d
      ccnv elex imaeq1 ineq1d eqeq2d rexbidv abbidv df-limsup xrltso cnvso mpbi
      cuz wor supex fvmpt syl ) DBFDGFDHIAJZDCJULIZKZLMZLNOZPZCQRZASZLNUAZOZPDB
      UBEDUQEJZURKZLMZLNOZPZCQRZASZLVEOVFGHVGDPZLVMVDVEVNVLVCAVNVKVBCQVNVJVAUQV
      NLVIUTNVNVHUSLVGDURUCUDTUEUFUGTEACUHLVDVELNUMLVEUMUILNUJUKUNUOUP $.
      $( [6-Nov-2013] $) $( [26-Oct-2005] $)

    $( Closure of the superior limit. $)
    limsupcl $p |- ( F e. A -> ( limsup ` F ) e. RR* ) $=
      ( vx vk wcel clsp cfv cv cuz cima cxr cin clt csup wceq cz wrex cab ax-mp
      wss ccnv limsupval inss2 supxrcl eleq1 mpbiri rexlimivw infmxrcl syl6eqel
      abssi ) BAEBFGCHZBDHIGJZKLZKMNZOZDPQZCRZKMUANZKCADBUBUQKTURKEUPCKUOUKKEZD
      PUOUSUNKEZUMKTUTULKUCUMUDSUKUNKUEUFUGUJUQUHSUI $.
      $( [11-Jun-2007] $) $( [26-Oct-2005] $)
  $}


