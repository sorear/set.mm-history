$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real and imaginary parts; conjugate
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Re $. $( Real part of a complex number. $)
  $c Im $. $( Imaginary part of a complex number. $)
  $c * $. $( Complex conjugate function. $)

  $( Extend class notation to include real part of a complex number. $)
  cre $a class Re $.

  $( Extend class notation to include imaginary part of a complex number. $)
  cim $a class Im $.

  $( Extend class notation to include complex conjugate function. $)
  ccj $a class * $.

  ${
    $d x y $.
    $( Define the complex conjugate function.  See ~ cjcli for its closure and
       ~ cjval for its value. $)
    df-cj $a |- * = ( x e. CC |-> ( iota_ y e. CC
                 ( ( x + y ) e. RR /\ ( _i x. ( x - y ) ) e. RR ) ) ) $.

    $( Define a function whose value is the real part of a complex number.  See
       ~ reval for its value, ~ recli for its closure, and ~ replim for its use
       in decomposing a complex number. $)
    df-re $a |- Re = ( x e. CC |-> ( ( x + ( * ` x ) ) / 2 ) ) $.

    $( Define a function whose value is the imaginary part of a complex
       number.  See ~ imval for its value, ~ imcli for its closure, and
       ~ replim for its use in decomposing a complex number. $)
    df-im $a |- Im = ( x e. CC |-> ( Re ` ( x / _i ) ) ) $.
  $}

  ${
    $d x y A $.
    $( The value of the conjugate of a complex number. $)
    cjval $p |- ( A e. CC -> ( * ` A ) = ( iota_ x e. CC
          ( ( A + x ) e. RR /\ ( _i x. ( A - x ) ) e. RR ) ) ) $=
      ( vy cv caddc co cr wcel ci cmin cmul wa cc crio wceq oveq1 eleq1d oveq2d
      ccj anbi12d riotabidv df-cj riotaex fvmpt ) CBCDZADZEFZGHZIUEUFJFZKFZGHZL
      ZAMNBUFEFZGHZIBUFJFZKFZGHZLZAMNMSUEBOZULURAMUSUHUNUKUQUSUGUMGUEBUFEPQUSUJ
      UPGUSUIUOIKUEBUFJPRQTUACAUBURAMUCUD $.
      $( [6-Nov-2013] $)

    $( The defining property of the complex conjugate. $)
    cjth $p |- ( A e. CC ->
          ( ( A + ( * ` A ) ) e. RR /\ ( _i x. ( A - ( * ` A ) ) ) e. RR ) ) $=
      ( vx cc wcel cv caddc co cr ci cmin cmul ccj cfv wsbc crio syl wceq oveq2
      wa eleq1d wreu cju riotasbc cjval dfsbcq mpbird fvex oveq2d anbi12d sbcie
      wb sylib ) ACDZABEZFGZHDZIAUNJGZKGZHDZSZBALMZNZAVAFGZHDZIAVAJGZKGZHDZSZUM
      VBUTBUTBCOZNZUMUTBCUAVJBAUBUTBCUCPUMVAVIQVBVJUKBAUDUTBVAVIUEPUFUTVHBVAALU
      GUNVAQZUPVDUSVGVKUOVCHUNVAAFRTVKURVFHVKUQVEIKUNVAAJRUHTUIUJUL $.
      $( [6-Nov-2013] $)

    $( Domain and codomain of the conjugate function. $)
    cjf $p |- * : CC --> CC $=
      ( vx vy cv caddc co cr wcel ci cmin cmul wa cc crio wral ccj wreu riotacl
      wf cju syl rgen df-cj fmpt mpbi ) ACZBCZDEFGHUEUFIEJEFGKZBLMZLGZALNLLORUI
      ALUELGUGBLPUIBUESUGBLQTUAALLUHOABUBUCUD $.
      $( [6-Nov-2013] $)

    $( The conjugate of a complex number is a complex number (closure law). $)
    cjcl $p |- ( A e. CC -> ( * ` A ) e. CC ) $=
      ( cc ccj cjf ffvelrni ) BBACDE $.
      $( [6-Nov-2013] $) $( [10-May-1999] $)

    $( The value of the real part of a complex number. $)
    reval $p |- ( A e. CC -> ( Re ` A ) = ( ( A + ( * ` A ) ) / 2 ) ) $=
      ( vx cv ccj cfv caddc co c2 cdiv cre wceq fveq2 oveq12 mpdan oveq1d df-re
      cc ovex fvmpt ) BABCZTDEZFGZHIGAADEZFGZHIGQJTAKZUBUDHIUEUAUCKUBUDKTADLTAU
      AUCFMNOBPUDHIRS $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The value of the imaginary part of a complex number. $)
    imval $p |- ( A e. CC -> ( Im ` A ) = ( Re ` ( A / _i ) ) ) $=
      ( vx cv ci cdiv co cre cfv cc cim wceq oveq1 fveq2d df-im fvex fvmpt ) BA
      BCZDEFZGHADEFZGHIJQAKRSGQADELMBNSGOP $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number in terms of the real part
       function. $)
    imre $p |- ( A e. CC -> ( Im ` A ) = ( Re ` ( -u _i x. A ) ) ) $=
      ( cc wcel cim cfv ci cdiv co cre cneg cmul imval cc0 wne wceq ax-icn ine0
      c1 divrec2 mp3an23 irec oveq1i syl6eq fveq2d eqtrd ) ABCZADEAFGHZIEFJZAKH
      ZIEALUFUGUIIUFUGRFGHZAKHZUIUFFBCFMNUGUKOPQAFSTUJUHAKUAUBUCUDUE $.
      $( [6-Nov-2013] $) $( [12-May-2005] $)

    $( The real part of a complex number is real. $)
    recl $p |- ( A e. CC -> ( Re ` A ) e. RR ) $=
      ( cc wcel cre cfv ccj caddc co c2 cdiv cr reval cmin cmul simpld rehalfcl
      ci cjth syl eqeltrd ) ABCZADEAAFEZGHZIJHZKALUAUCKCZUDKCUAUEQAUBMHNHKCAROU
      CPST $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number is real. $)
    imcl $p |- ( A e. CC -> ( Im ` A ) e. RR ) $=
      ( cc wcel cim cfv ci cneg cmul co cre cr imre ax-icn negcli ax-mulcl mpan
      recl syl eqeltrd ) ABCZADEFGZAHIZJEZKALTUBBCZUCKCUABCTUDFMNUAAOPUBQRS $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( Domain and codomain of the real part function.  (Contributed by Paul
       Chapman, 22-Oct-2007.) $)
    ref $p |- Re : CC --> RR $=
      ( vx cv ccj cfv caddc co c2 cdiv cr wcel cc wral reval recl eqeltrrd rgen
      cre wf df-re fmpt mpbi ) ABZUBCDEFGHFZIJZAKLKIQRUDAKUBKJUBQDUCIUBMUBNOPAK
      IUCQASTUA $.
      $( [6-Nov-2013] $) $( [22-Oct-2007] $)

    $( Domain and codomain of the imaginary part function.  (Contributed by
       Paul Chapman, 22-Oct-2007.) $)
    imf $p |- Im : CC --> RR $=
      ( vx cv ci cdiv co cre cfv cr wcel cc wral imval imcl eqeltrrd rgen df-im
      cim wf fmpt mpbi ) ABZCDEFGZHIZAJKJHQRUCAJUAJIUAQGUBHUALUAMNOAJHUBQAPST
      $.
      $( [6-Nov-2013] $) $( [22-Oct-2007] $)

    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crre $p |- ( ( A e. RR /\ B e. RR ) -> ( Re ` ( A + ( _i x. B ) ) ) = A )
        $=
      ( cr wcel ci cmul co caddc c2 cdiv cc wceq ax-icn sylancr syl syl2anc a1i
      cmin syl3anc oveq1d wa cre cfv ccj recn mulcl addcl syl2an reval cc0 recl
      eqeltrrd simpl resubcl adantl subcl halfcl adantr ax-mulcl pnpcan pnpcan2
      cjcl subdi eqtr4d ax-addcl addsub subsub 3eqtr4d 2times wne 2cn divsubdir
      oveq2d 2ne0 syl112anc 3eqtr3d divcan3 mulass divass oveq12d cneg renegcli
      c1 ixi eqeltri simpr ax-mulrcl cjth simprd rehalfcl 3syl eqeltrd rimul wb
      1re subeq0 mpbid eqtrd ) ACDZBCDZUAZAEBFGZHGZUBUCZXCXCUDUCZHGZIJGZAXAXCKD
      ZXDXGLWSAKDZXBKDZXHWTAUEZWTEKDZBKDZXJMBUEZEBUFZNAXBUGUHZXCUIOZXAXGARGZUJL
      ZXGALZXAXRCDZEXRFGZCDXSXAXGCDWSYAXAXDXGCXQXAXHXDCDXPXCUKOULWSWTUMXGAUNPXA
      YBEEFGZBFGZEXCXERGZFGZIJGZRGZCXAEXBYEIJGZRGZFGZEXBFGZEYIFGZRGZYBYHXAXLXJY
      IKDZYKYNLXLXAMQZXAXLXMXJMWTXMWSXNUOZXONXAYEKDZYOXAXHXEKDZYRXPXAXHYSXPXCVB
      OZXCXEUPPZYEUQOEXBYIVCSXAXRYJEFXAXGIAFGZIJGZRGZIXBFGZIJGZYIRGZXRYJXAXFUUB
      RGZIJGZUUEYERGZIJGZUUDUUGXAUUHUUJIJXAXFAAHGZRGZXBXBHGZYERGZUUHUUJXAXCUULR
      GZXEHGZUUNXCRGZXEHGZUUMUUOXAUUPUURXEHXAUUPXBARGZUURXAXIXJXIUUPUUTLWSXIWTX
      KURZXAXLXMXJMYQEBUSNZUVAAXBAUTSXAXJXIXJUURUUTLUVBUVAUVBXBAXBVASVDTXAXHYSU
      ULKDZUUMUUQLXPYTXAXIXIUVCUVAUVAAAVEPXCXEUULVFSXAUUNKDZXHYSUUOUUSLXAXJXJUV
      DUVBUVBXBXBVEPXPYTUUNXCXEVGSVHXAUUBUULXFRXAXIUUBUULLUVAAVIOVMXAUUEUUNYERX
      AXJUUEUUNLUVBXBVIOTVHTXAXFKDZUUBKDZIKDZIUJVJZUUIUUDLXAXHYSUVEXPYTXCXEVEPZ
      XAUVGXIUVFVKUVAIAUSNUVGXAVKQZUVHXAVNQZXFUUBIVLVOXAUUEKDZYRUVGUVHUUKUUGLXA
      UVGXJUVLVKUVBIXBUSNUUAUVJUVKUUEYEIVLVOVPXAUUCAXGRXAXIUVGUVHUUCALUVAUVJUVK
      AIVQSVMXAUUFXBYIRXAXJUVGUVHUUFXBLUVBUVJUVKXBIVQSTVPVMXAYDYLYGYMRXAXLXLXMY
      DYLLYPYPYQEEBVRSXAXLYRUVGUVHYGYMLYPUUAUVJUVKEYEIVSVOVTVHXAYDCDZYGCDZYHCDX
      AYCCDWTUVMYCWCWACWDWCWOWBWEWSWTWFYCBWGNXAXHYFCDZUVNXPXHXFCDUVOXCWHWIYFWJW
      KYDYGUNPWLXRWMPXAXGKDZXIXSXTWNXAUVEUVPUVIXFUQOUVAXGAWPPWQWR $.
      $( [7-Nov-2013] $) $( [12-May-2005] $)

    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crim $p |- ( ( A e. RR /\ B e. RR ) -> ( Im ` ( A + ( _i x. B ) ) ) = B )
        $=
      ( cr wcel wa ci cmul co caddc cfv cdiv cre cneg wceq recn ax-icn ax-mulcl
      cc sylancr ine0 cim ax-addcl syl2an imval syl mpan cc0 wne divdir mpanr12
      3expa sylan2 c1 divrec2 mp3an23 irec oveq1i a1i mulneg12 3eqtrd oveqan12d
      divcan3 negcl addcom sylan 3eqtrrd fveq2d id renegcl crre syl2anr 3eqtr2d
      ) ACDZBCDZEZAFBGHZIHZUAJZVQFKHZLJZBFAMZGHZIHZLJZBVOVQRDZVRVTNVMARDZVPRDZW
      EVNAOZVNFRDZBRDZWGPBOZFBQZSAVPUBUCVQUDUEVOWCVSLVMWFWJWCVSNVNWHWKWFWJEVSAF
      KHZVPFKHZIHZWBBIHZWCWJWFWGVSWONZWIWJWGPWLUFWFWGEWIFUGUHZWQPTWFWGWIWREWQAV
      PFUIUKUJULWFWJWMWBWNBIWFWMUMFKHZAGHZFMZAGHZWBWFWIWRWMWTNPTAFUNUOWTXBNWFWS
      XAAGUPUQURWIWFXBWBNPFAUSUFUTWJWIWRWNBNPTBFVBUOVAWFWBRDZWJWPWCNWFWIWARDXCP
      AVCFWAQSWBBVDVEVFUCVGVNVNWACDWDBNVMVNVHAVIBWAVJVKVL $.
      $( [7-Nov-2013] $) $( [12-May-2005] $)

    $( Reconstruct a complex number from its real and imaginary parts. $)
    replim $p |- ( A e. CC -> A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cc wcel cv ci cmul co caddc wceq wrex cre cfv cim ax-cnre oveq12d
      cr oveq2d fveq2 wa crre crim eqcomd id eqeq12d syl5ibrcom rexlimivv syl )
      ADEABFZGCFZHIZJIZKZCRLBRLAAMNZGAONZHIZJIZKZBCAPUNUSBCRRUJREUKREUAZUSUNUMU
      MMNZGUMONZHIZJIZKUTVDUMUTVAUJVCULJUJUKUBUTVBUKGHUJUKUCSQUDUNAUMURVDUNUEUN
      UOVAUQVCJAUMMTUNUPVBGHAUMOTSQUFUGUHUI $.
      $( [7-Nov-2013] $) $( [10-May-1999] $)

    $( Value of the conjugate of a complex number.  The value is the real part
       minus ` _i ` times the imaginary part.  Definition 10-3.2 of [Gleason]
       p. 132. $)
    remim $p |- ( A e. CC ->
                      ( * ` A ) = ( ( Re ` A ) - ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cc wcel cfv cv caddc co cr ci cmin cmul wceq oveq1d recnd syl3anc
      wa ax-icn syl2anc ccj crio cre cim cjval replim recl imcl ax-mulcl ppncan
      sylancr eqtrd readdcl eqeltrd pnncan a1i adddi 3eqtr4d oveq2d mp3an12 syl
      mulass eqtr4d c1 cneg ixi 1re renegcli eqeltri ax-mulrcl wreu subcl ax-17
      wb cju a17d oveq2 eleq1d anbi12d riota2f mpbi2and ) ADEZAUAFABGZHIZJEZKAW
      CLIZMIZJEZRZBDUBZAUCFZKAUDFZMIZLIZBAUEWBAWNHIZJEZKAWNLIZMIZJEZWJWNNZWBWOW
      KWKHIZJWBWOWKWMHIZWNHIZXAWBAXBWNHAUFZOWBWKDEZWMDEZXEXCXANWBWKAUGZPZWBKDEZ
      WLDEZXFSWBWLAUHZPZKWLUIUKZXHWKWMWKUJQULWBWKJEZXNXAJEXGXGWKWKUMTUNWBWRKKMI
      ZWLWLHIZMIZJWBWRKKXPMIZMIZXQWBWQXRKMWBXBWNLIZWMWMHIZWQXRWBXEXFXFXTYANXHXM
      XMWKWMWMUOQWBAXBWNLXDOWBXIXJXJXRYANXIWBSUPXLXLKWLWLUQQURUSWBXPDEZXQXSNZWB
      XPWBWLJEZYDXPJEZXKXKWLWLUMTZPXIXIYBYCSSKKXPVBUTVAVCWBXOJEYEXQJEXOVDVEJVFV
      DVGVHVIYFXOXPVJUKUNWBWNDEZWIBDVKWPWSRZWTVNWBXEXFYGXHXMWKWMVLTBAVOWIYHBCDW
      NCGWNEBVMYGYHBVPWCWNNZWEWPWHWSYIWDWOJWCWNAHVQVRYIWGWRJYIWFWQKMWCWNALVQUSV
      RVSVTTWAUL $.
      $( [7-Nov-2013] $) $( [10-May-1999] $)
  $}

  ${
    recl.1 $e |- A e. CC $.
    $( The real part of a complex number is real (closure law). $)
    recli $p |- ( Re ` A ) e. RR $=
      ( cc wcel cre cfv cr recl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( The imaginary part of a complex number is real (closure law). $)
    imcli $p |- ( Im ` A ) e. RR $=
      ( cc wcel cim cfv cr imcl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( Closure law for complex conjugate. $)
    cjcli $p |- ( * ` A ) e. CC $=
      ( cc wcel ccj cfv cjcl ax-mp ) ACDAEFCDBAGH $.
      $( [11-May-1999] $)

    $( Construct a complex number from its real and imaginary parts. $)
    replimi $p |- A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) $=
      ( cc wcel cre cfv ci cim cmul co caddc wceq replim ax-mp ) ACDAAEFGAHFIJK
      JLBAMN $.
      $( [11-Mar-2008] $) $( [1-Oct-1999] $)
  $}

  ${
    crre.1 $e |- A e. RR $.
    crre.2 $e |- B e. RR $.
    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crrei $p |- ( Re ` ( A + ( _i x. B ) ) ) = A $=
      ( cr wcel ci cmul co caddc cre cfv wceq crre mp2an ) AEFBEFAGBHIJIKLAMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)

    $( The imaginary part of a complex number representation.  Definition
       10-3.1 of [Gleason] p. 132. $)
    crimi $p |- ( Im ` ( A + ( _i x. B ) ) ) = B $=
      ( cr wcel ci cmul co caddc cim cfv wceq crim mp2an ) AEFBEFAGBHIJIKLBMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)
  $}

  $( The imaginary part of a real number is 0. $)
  reim0 $p |- ( A e. RR -> ( Im ` A ) = 0 ) $=
    ( cr wcel ci cc0 cmul co caddc cim cc wceq recn ax-icn mul01i oveq2i addid1
    cfv syl5eq syl fveq2d 0re crim mpan2 eqtr3d ) ABCZADEFGZHGZIQZAIQEUEUGAIUEA
    JCZUGAKALUIUGAEHGAUFEAHDMNOAPRSTUEEBCUHEKUAAEUBUCUD $.
    $( [7-Nov-2013] $) $( [18-Mar-2005] $)

  $( A number is real iff its imaginary part is 0. $)
  reim0b $p |- ( A e. CC -> ( A e. RR <-> ( Im ` A ) = 0 ) ) $=
    ( cc wcel cr cim cfv cc0 wceq reim0 wa ci cmul co caddc replim adantr oveq2
    cre ax-icn mul01i syl6eq oveq2d recnd addid1 syl sylan9eqr eqtrd eqeltrd ex
    recl impbid2 ) ABCZADCZAEFZGHZAIULUOUMULUOJZAARFZDUPAUQKUNLMZNMZUQULAUSHUOA
    OPUOULUSUQGNMZUQUOURGUQNUOURKGLMGUNGKLQKSTUAUBULUQBCUTUQHULUQAUJZUCUQUDUEUF
    UGULUQDCUOVAPUHUIUK $.
    $( [24-Jul-2008] $) $( [26-Sep-2005] $)

  $( A number is real iff it equals its real part.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  rereb $p |- ( A e. CC -> ( A e. RR <-> ( Re ` A ) = A ) ) $=
    ( cc wcel cr cre cfv wceq wa ci cim co caddc cc0 replim adantr reim0 oveq2d
    cmul ax-icn mul01i syl6eq adantl recl recnd addid1 3eqtrrd eqeltrrd impbida
    syl simpr ) ABCZADCZAEFZAGZUKULHZAUMIAJFZRKZLKZUMMLKZUMUKAURGULANOUOUQMUMLU
    LUQMGUKULUQIMRKMULUPMIRAPQISTUAUBQUKUSUMGZULUKUMBCUTUKUMAUCZUDUMUEUIOUFUKUN
    HUMADUKUNUJUKUMDCUNVAOUGUH $.
    $( [20-Aug-2008] $)

  $( A product with a nonzero real multiplier is real iff the multiplicand is
     real. $)
  mulre $p |- ( ( A e. CC /\ B e. RR /\ B =/= 0 )
         -> ( A e. RR <-> ( B x. A ) e. RR ) ) $=
    ( cc wcel cr cre cfv wceq cmul co wb rereb 3ad2ant1 wa recnd syl3anc adantl
    ci caddc ax-icn cc0 wne w3a recl recn anim1i 3adant1 mulcan cim adantr imcl
    simp1 mulcl sylancr adddi replim oveq2d mul12 mp3an1 syl2an 3eqtr4d remulcl
    fveq2d sylan2 syl2anc eqtr2d eqeq1d sylan syl bitr4d ancoms 3adant3 3bitr2d
    crre ) ACDZBEDZBUAUBZUCZAEDZAFGZAHZBVTIJZBAIJZHZWCEDZVOVPVSWAKVQALMVRVTCDZV
    OBCDZVQNZWDWAKVOVPWFVQVOVTAUDZOZMVOVPVQULVPVQWHVOVPWGVQBUEZUFUGVTABUHPVOVPW
    DWEKZVQVPVOWLVPVONZWDWCFGZWCHZWEWMWBWNWCWMWNWBRBAUIGZIJZIJZSJZFGZWBWMWCWSFW
    MBVTRWPIJZSJZIJZWBBXAIJZSJZWCWSWMWGWFXACDZXCXEHVPWGVOWKUJVOWFVPWJQVOXFVPVOR
    CDZWPCDZXFTVOWPAUKZOZRWPUMUNQBVTXAUOPWMAXBBIVOAXBHVPAUPQUQWMWRXDWBSVPWGXHWR
    XDHZVOWKXJXGWGXHXKTRBWPURUSUTUQVAVCWMWBEDZWQEDZWTWBHVOVPVTEDXLWIBVTVBVDVOVP
    WPEDXMXIBWPVBVDWBWQVNVEVFVGWMWCCDZWEWOKVPWGVOXNWKBAUMVHWCLVIVJVKVLVM $.
    $( [21-Aug-2008] $)

  $( A real number equals its real part.  One direction of Proposition
     10-3.4(f) of [Gleason] p. 133.  (Contributed by Paul Chapman,
     7-Sep-2007.) $)
  rere $p |- ( A e. RR -> ( Re ` A ) = A ) $=
    ( cr wcel cre cfv wceq cc wb recn rereb syl ibi ) ABCZADEAFZMAGCMNHAIAJKL
    $.
    $( [7-Sep-2007] $)

  $( A number is real iff it equals its complex conjugate.  Proposition
     10-3.4(f) of [Gleason] p. 133. $)
  cjreb $p |- ( A e. CC -> ( A e. RR <-> ( * ` A ) = A ) ) $=
    ( cc wcel ccj cfv wceq cre ci cneg cmul co caddc recnd ax-icn mulcl sylancr
    wb syl syl3anc cc0 cr cmin recl imcl negsub syl2anc mulneg2 oveq2d 3eqtr4rd
    cim remim replim eqeq12d negcl addcan eqcom eqneg syl5bb wne wa ine0 pm3.2i
    a1i mulcan reim0b 3bitr4d 3bitrrd ) ABCZADEZAFAGEZHAUJEZIZJKZLKZVJHVKJKZLKZ
    FZVMVOFZAUACZVHVIVNAVPVHVJVOIZLKZVJVOUBKZVNVIVHVJBCZVOBCZWAWBFVHVJAUCMZVHHB
    CZVKBCZWDNVHVKAUDMZHVKOPZVJVOUEUFVHVMVTVJLVHWFWGVMVTFNWHHVKUGPUHAUKUIAULUMV
    HWCVMBCZWDVQVRQWEVHWFVLBCZWJNVHWGWKWHVKUNRZHVLOPWIVJVMVOUOSVHVLVKFZVKTFZVRV
    SWMVKVLFZVHWNVLVKUPVHWGWOWNQWHVKUQRURVHWKWGWFHTUSZUTZVRWMQWLWHWQVHWFWPNVAVB
    VCVLVKHVDSAVEVFVG $.
    $( [14-Jul-2014] $) $( [2-Jul-2005] $)

  $( Real part of a complex conjugate. $)
  recj $p |- ( A e. CC -> ( Re ` ( * ` A ) ) = ( Re ` A ) ) $=
    ( cc wcel ccj cfv cre ci cim cneg cmul co caddc cmin wceq recl recnd ax-icn
    sylancr syl2anc cr imcl mulcl negsub mulneg2 oveq2d 3eqtr4rd fveq2d renegcl
    remim syl crre eqtrd ) ABCZADEZFEAFEZGAHEZIZJKZLKZFEZUOUMUNUSFUMUOGUPJKZIZL
    KZUOVAMKZUSUNUMUOBCVABCZVCVDNUMUOAOZPUMGBCZUPBCZVEQUMUPAUAZPZGUPUBRUOVAUCSU
    MURVBUOLUMVGVHURVBNQVJGUPUDRUEAUIUFUGUMUOTCUQTCZUTUONVFUMUPTCVKVIUPUHUJUOUQ
    UKSUL $.
    $( [14-Jul-2014] $)

  $( Real part of negative. $)
  reneg $p |- ( A e. CC -> ( Re ` -u A ) = -u ( Re ` A ) ) $=
    ( cc wcel cneg cre cfv ci cim cmul co caddc wceq recl recnd sylancr syl2anc
    ax-icn cr renegcl syl imcl mulcl negdi replim negeqd mulneg2 oveq2d 3eqtr4d
    fveq2d crre eqtrd ) ABCZADZEFAEFZDZGAHFZDZIJZKJZEFZUOULUMUSEULUNGUPIJZKJZDZ
    UOVADZKJZUMUSULUNBCVABCZVCVELULUNAMZNULGBCZUPBCZVFQULUPAUAZNZGUPUBOUNVAUCPU
    LAVBAUDUEULURVDUOKULVHVIURVDLQVKGUPUFOUGUHUIULUORCZUQRCZUTUOLULUNRCVLVGUNST
    ULUPRCVMVJUPSTUOUQUJPUK $.
    $( [14-Jul-2014] $) $( [17-Mar-2005] $)

  $( Real part distributes over addition. $)
  readd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) ) $=
    ( cc wcel caddc co cre cfv ci cim cmul wceq cr recl adantr recnd imcl mulcl
    ax-icn sylancr wa adantl syl22anc replim oveqan12d a1i adddi syl3anc oveq2d
    add4 3eqtr4d fveq2d readdcl syl2an crre syl2anc eqtrd ) ACDZBCDZUAZABEFZGHA
    GHZBGHZEFZIAJHZBJHZEFZKFZEFZGHZVDUTVAVIGUTVBIVEKFZEFZVCIVFKFZEFZEFZVDVKVMEF
    ZEFZVAVIUTVBCDVKCDZVCCDVMCDZVOVQLUTVBURVBMDZUSANZOPUTICDZVECDZVRSUTVEURVEMD
    ZUSAQZOPZIVERTUTVCUSVCMDZURBNZUBPUTWBVFCDZVSSUTVFUSVFMDZURBQZUBPZIVFRTVBVKV
    CVMUJUCURUSAVLBVNEAUDBUDUEUTVHVPVDEUTWBWCWIVHVPLWBUTSUFWFWLIVEVFUGUHUIUKULU
    TVDMDZVGMDZVJVDLURVTWGWMUSWAWHVBVCUMUNURWDWJWNUSWEWKVEVFUMUNVDVGUOUPUQ $.
    $( [14-Jul-2014] $) $( [17-Mar-2005] $)

  $( Real part distributes over subtraction. $)
  resub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A - B ) ) = ( ( Re ` A ) - ( Re ` B ) ) ) $=
    ( cc wcel wa cneg caddc cre cfv cmin negcl readd sylan2 reneg adantl negsub
    co wceq recl recnd oveq2d eqtrd fveq2d syl2an 3eqtr3d ) ACDZBCDZEZABFZGQZHI
    ZAHIZBHIZFZGQZABJQZHIULUMJQZUHUKULUIHIZGQZUOUGUFUICDUKUSRBKAUILMUHURUNULGUG
    URUNRUFBNOUAUBUHUJUPHABPUCUFULCDUMCDUOUQRUGUFULASTUGUMBSTULUMPUDUE $.
    $( [2-Aug-2006] $) $( [17-Mar-2005] $)

  $( Lemma for ~ remul , ~ immul , and ~ cjmul . $)
  remullem $p |- ( ( A e. CC /\ B e. CC ) ->
    ( ( Re ` ( A x. B ) ) =
        ( ( ( Re ` A ) x. ( Re ` B ) ) - ( ( Im ` A ) x. ( Im ` B ) ) ) /\
      ( Im ` ( A x. B ) ) =
        ( ( ( Re ` A ) x. ( Im ` B ) ) + ( ( Im ` A ) x. ( Re ` B ) ) ) /\
      ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) ) ) $=
    ( cc wcel cmul co cre cfv cim cmin wceq caddc ci recnd ax-icn mulcl syl2anc
    cr syl3anc oveq12d wa ccj replim oveqan12d recl adantr sylancr addcl adantl
    imcl adddi adddir remulcl add42 syl22anc cneg a1i mul4 ixi oveq1i mulm1 syl
    c1 syl5eq oveq2d negsub addcom mulass 3eqtr4d 3eqtr2d 3eqtrd fveq2d resubcl
    eqtrd mul12 readdcl crre crim subcl subdir subadd4 subdi eqtr2d remim 3jca
    ) ACDZBCDZUAZABEFZGHZAGHZBGHZEFZAIHZBIHZEFZJFZKWIIHZWKWOEFZWNWLEFZLFZKWIUBH
    ZAUBHZBUBHZEFZKWHWJWQMXAEFZLFZGHZWQWHWIXGGWHWIWKMWNEFZLFZWLMWOEFZLFZEFZXJWL
    EFZXJXKEFZLFZXGWFWGAXJBXLEAUCBUCUDWHXJCDZWLCDZXKCDZXMXPKWHWKCDZXICDZXQWHWKW
    FWKRDZWGAUEUFZNZWHMCDZWNCDZYAOWHWNWFWNRDZWGAUJUFZNZMWNPUGZWKXIUHQWHWLWGWLRD
    ZWFBUEUIZNZWHYEWOCDZXSOWHWOWGWORDZWFBUJUIZNZMWOPUGZXJWLXKUKSWHXPWMXIWLEFZLF
    ZWKXKEFZXIXKEFZLFZLFZWMUUBLFZYSUUALFZLFZXGWHXNYTXOUUCLWHXTYAXRXNYTKYDYJYMWK
    XIWLULSWHXTYAXSXOUUCKYDYJYRWKXIXKULSTWHWMCDZUUBCDZYSCDZUUACDZUUGUUDKWHWMWHY
    BYKWMRDZYCYLWKWLUMQZNZWHYAXSUUIYJYRXIXKPQZWHYAXRUUJYJYMXIWLPQZWHXTXSUUKYDYR
    WKXKPQZWMUUBYSUUAUNUOWHUUEWQUUFXFLWHUUEWMWPUPZLFZWQWHUUBUURWMLWHUUBMMEFZWPE
    FZUURWHYEYFYEYNUUBUVAKYEWHOUQZYIUVBYQMWNMWOURUOWHUVAVCUPZWPEFZUURUUTUVCWPEU
    SUTWHWPCDZUVDUURKWHWPWHYGYOWPRDZYHYPWNWOUMQZNZWPVAVBVDVNVEWHUUHUVEUUSWQKUUN
    UVHWMWPVFQVNZWHMWTEFZMWSEFZLFZUVKUVJLFZUUFXFWHUVJCDZUVKCDZUVLUVMKWHYEWTCDZU
    VNOWHWTWHYGYKWTRDZYHYLWNWLUMQZNZMWTPUGWHYEWSCDZUVOOWHWSWHYBYOWSRDZYCYPWKWOU
    MQZNZMWSPUGUVJUVKVGQWHYSUVJUUAUVKLWHYEYFXRYSUVJKUVBYIYMMWNWLVHSZWHXTYEYNUUA
    UVKKYDUVBYQWKMWOVOSZTWHYEUVTUVPXFUVMKUVBUWCUVSMWSWTUKSZVITVJVKZVLZWHWQRDZXA
    RDZXHWQKWHUULUVFUWIUUMUVGWMWPVMQZWHUWAUVQUWJUWBUVRWSWTVPQZWQXAVQQZVNWHWRXGI
    HZXAWHWIXGIUWGVLWHUWIUWJUWNXAKUWKUWLWQXAVRQVNZWHWJMWREFZJFZWKXIJFZWLXKJFZEF
    ZXBXEWHUWTWKUWSEFZXIUWSEFZJFZUWQWHXTYAUWSCDZUWTUXCKYDYJWHXRXSUXDYMYRWLXKVSQ
    WKXIUWSVTSWHWMUUAJFZYSUUBJFZJFZUUEUUAYSLFZJFZUXCUWQWHUUHUUKUUJUUIUXGUXIKUUN
    UUQUUPUUOWMUUAYSUUBWAUOWHUXAUXEUXBUXFJWHXTXRXSUXAUXEKYDYMYRWKWLXKWBSWHYAXRX
    SUXBUXFKYJYMYRXIWLXKWBSTWHWJUUEUWPUXHJWHXHWQWJUUEUWMUWHUVIVIWHXFUVMUWPUXHUW
    FWHWRXAMEUWOVEWHUUAUVKYSUVJLUWEUWDTVITVIWCWHWICDXBUWQKABPWIWDVBWFWGXCUWRXDU
    WSEAWDBWDUDVIWE $.
    $( [14-Jul-2014] $) $( [28-Jul-1999] $)

  $( Real part of a product. $)
  remul $p |- ( ( A e. CC /\ B e. CC ) -> ( Re ` ( A x. B ) ) =
           ( ( ( Re ` A ) x. ( Re ` B ) ) - ( ( Im ` A ) x. ( Im ` B ) ) ) ) $=
    ( cc wcel wa cmul co cre cfv cim cmin wceq caddc ccj remullem simp1d ) ACDB
    CDEABFGZHIAHIZBHIZFGAJIZBJIZFGKGLQJIRUAFGTSFGMGLQNIANIBNIFGLABOP $.
    $( [14-Jul-2014] $) $( [28-Jul-1999] $)
  $( Real part of a product.  (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  remul2 $p |- ( ( A e. RR /\ B e. CC ) ->
                 ( Re ` ( A x. B ) ) = ( A x. ( Re ` B ) ) ) $=
    ( cr wcel cc wa cmul co cre cfv cim cmin wceq recn remul sylan oveq1d recnd
    cc0 syl rere adantr reim0 mul02 sylan9eq oveq12d mulcl syl2an subid1 3eqtrd
    imcl recl ) ACDZBEDZFZABGHIJZAIJZBIJZGHZAKJZBKJZGHZLHZAURGHZSLHZVDUMAEDZUNU
    PVCMANZABOPUOUSVDVBSLUOUQAURGUMUQAMUNAUAUBQUMUNVBSVAGHZSUMUTSVAGAUCQUNVAEDV
    HSMUNVABUKRVAUDTUEUFUOVDEDZVEVDMUMVFUREDVIUNVGUNURBULRAURUGUHVDUITUJ $.
    $( [2-Aug-2014] $)

  $( Imaginary part of a complex conjugate. $)
  imcj $p |- ( A e. CC -> ( Im ` ( * ` A ) ) = -u ( Im ` A ) ) $=
    ( cc wcel ccj cfv cim cre ci cneg cmul co caddc cmin wceq recl recnd ax-icn
    sylancr syl2anc cr imcl mulcl negsub mulneg2 oveq2d 3eqtr4rd fveq2d renegcl
    remim syl crim eqtrd ) ABCZADEZFEAGEZHAFEZIZJKZLKZFEZUQUMUNUSFUMUOHUPJKZIZL
    KZUOVAMKZUSUNUMUOBCVABCZVCVDNUMUOAOZPUMHBCZUPBCZVEQUMUPAUAZPZHUPUBRUOVAUCSU
    MURVBUOLUMVGVHURVBNQVJHUPUDRUEAUIUFUGUMUOTCUQTCZUTUQNVFUMUPTCVKVIUPUHUJUOUQ
    UKSUL $.
    $( [14-Jul-2014] $) $( [18-Mar-2005] $)

  $( The imaginary part of a negative number. $)
  imneg $p |- ( A e. CC -> ( Im ` -u A ) = -u ( Im ` A ) ) $=
    ( cc wcel cneg cim cfv cre ci cmul co caddc wceq recl recnd sylancr syl2anc
    ax-icn cr renegcl syl imcl mulcl negdi replim negeqd mulneg2 oveq2d 3eqtr4d
    fveq2d crim eqtrd ) ABCZADZEFAGFZDZHAEFZDZIJZKJZEFZUQULUMUSEULUNHUPIJZKJZDZ
    UOVADZKJZUMUSULUNBCVABCZVCVELULUNAMZNULHBCZUPBCZVFQULUPAUAZNZHUPUBOUNVAUCPU
    LAVBAUDUEULURVDUOKULVHVIURVDLQVKHUPUFOUGUHUIULUORCZUQRCZUTUQLULUNRCVLVGUNST
    ULUPRCVMVJUPSTUOUQUJPUK $.
    $( [14-Jul-2014] $) $( [18-Mar-2005] $)

  $( Imaginary part distributes over addition. $)
  imadd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) ) $=
    ( cc wcel caddc co cim cfv cre ci cmul wceq cr recl adantr recnd imcl mulcl
    ax-icn sylancr wa adantl syl22anc replim oveqan12d a1i adddi syl3anc oveq2d
    add4 3eqtr4d fveq2d readdcl syl2an crim syl2anc eqtrd ) ACDZBCDZUAZABEFZGHA
    IHZBIHZEFZJAGHZBGHZEFZKFZEFZGHZVGUTVAVIGUTVBJVEKFZEFZVCJVFKFZEFZEFZVDVKVMEF
    ZEFZVAVIUTVBCDVKCDZVCCDVMCDZVOVQLUTVBURVBMDZUSANZOPUTJCDZVECDZVRSUTVEURVEMD
    ZUSAQZOPZJVERTUTVCUSVCMDZURBNZUBPUTWBVFCDZVSSUTVFUSVFMDZURBQZUBPZJVFRTVBVKV
    CVMUJUCURUSAVLBVNEAUDBUDUEUTVHVPVDEUTWBWCWIVHVPLWBUTSUFWFWLJVEVFUGUHUIUKULU
    TVDMDZVGMDZVJVGLURVTWGWMUSWAWHVBVCUMUNURWDWJWNUSWEWKVEVFUMUNVDVGUOUPUQ $.
    $( [14-Jul-2014] $) $( [18-Mar-2005] $)

  $( Imaginary part distributes over subtraction. $)
  imsub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A - B ) ) = ( ( Im ` A ) - ( Im ` B ) ) ) $=
    ( cc wcel wa cneg caddc cim cfv cmin negcl imadd sylan2 imneg adantl negsub
    co wceq imcl recnd oveq2d eqtrd fveq2d syl2an 3eqtr3d ) ACDZBCDZEZABFZGQZHI
    ZAHIZBHIZFZGQZABJQZHIULUMJQZUHUKULUIHIZGQZUOUGUFUICDUKUSRBKAUILMUHURUNULGUG
    URUNRUFBNOUAUBUHUJUPHABPUCUFULCDUMCDUOUQRUGUFULASTUGUMBSTULUMPUDUE $.
    $( [1-Aug-2006] $) $( [18-Mar-2005] $)
  $( Imaginary part of a product. $)
  immul $p |- ( ( A e. CC /\ B e. CC ) -> ( Im ` ( A x. B ) ) =
           ( ( ( Re ` A ) x. ( Im ` B ) ) + ( ( Im ` A ) x. ( Re ` B ) ) ) ) $=
    ( cc wcel wa cmul co cre cfv cim cmin wceq caddc ccj remullem simp2d ) ACDB
    CDEABFGZHIAHIZBHIZFGAJIZBJIZFGKGLQJIRUAFGTSFGMGLQNIANIBNIFGLABOP $.
    $( [14-Jul-2014] $) $( [28-Jul-1999] $)
  $( Imaginary part of a product. $)
  immul2 $p |- ( ( A e. RR /\ B e. CC ) ->
                 ( Im ` ( A x. B ) ) = ( A x. ( Im ` B ) ) ) $=
    ( cr wcel cc wa cmul co cim cfv cre caddc cc0 wceq immul sylan oveq1d recnd
    recn syl rere adantr reim0 recl mul02 sylan9eq oveq12d syl2an addid1 3eqtrd
    imcl mulcl ) ACDZBEDZFZABGHIJZAKJZBIJZGHZAIJZBKJZGHZLHZAURGHZMLHZVDUMAEDZUN
    UPVCNASZABOPUOUSVDVBMLUOUQAURGUMUQANUNAUAUBQUMUNVBMVAGHZMUMUTMVAGAUCQUNVAED
    VHMNUNVABUDRVAUETUFUGUOVDEDZVEVDNUMVFUREDVIUNVGUNURBUKRAURULUHVDUITUJ $.
    $( [2-Aug-2014] $)

  $( A real number equals its complex conjugate.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  cjre $p |- ( A e. RR -> ( * ` A ) = A ) $=
    ( cc wcel cr ccj cfv wceq recn cjreb biimpd mpcom ) ABCZADCZAEFAGZAHLMNAIJK
    $.
    $( [8-Oct-1999] $)

  $( The conjugate of the conjugate is the original complex number.
     Proposition 10-3.4(e) of [Gleason] p. 133.  (The proof was shortened by
     Mario Carneiro, 14-Jul-2014.) $)
  cjcj $p |- ( A e. CC -> ( * ` ( * ` A ) ) = A ) $=
    ( cc wcel ccj cfv cre ci cim cmul co caddc wceq cjcl recj eqtrd cneg negeqd
    syl imcj replim imcl recnd negneg 3eqtrd oveq2d oveq12d 3syl 3eqtr4d ) ABCZ
    ADEZDEZFEZGUKHEZIJZKJZAFEZGAHEZIJZKJUKAUIULUPUNURKUIULUJFEZUPUIUJBCZULUSLAM
    ZUJNRANOUIUMUQGIUIUMUJHEZPZUQPZPZUQUIUTUMVCLVAUJSRUIVBVDASQUIUQBCVEUQLUIUQA
    UAUBUQUCRUDUEUFUIUTUKBCUKUOLVAUJMUKTUGATUH $.
    $( [14-Jul-2014] $) $( [29-Jul-1999] $)

  $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
     [Gleason] p. 133. $)
  cjadd $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) ) $=
    ( cc wcel caddc co cre cfv ci cim cmul cmin ccj wceq ax-icn cr adantr recnd
    imcl remim wa readd imadd oveq2d a1i adddi syl3anc eqtrd oveq12d recl mulcl
    adantl sylancr addsub4 syl22anc addcl syl oveqan12d 3eqtr4d ) ACDZBCDZUAZAB
    EFZGHZIVCJHZKFZLFZAGHZIAJHZKFZLFZBGHZIBJHZKFZLFZEFZVCMHZAMHZBMHZEFVBVGVHVLE
    FZVJVNEFZLFZVPVBVDVTVFWALABUBVBVFIVIVMEFZKFZWAVBVEWCIKABUCUDVBICDZVICDZVMCD
    ZWDWANWEVBOUEVBVIUTVIPDVAASQRZVBVMVAVMPDUTBSULRZIVIVMUFUGUHUIVBVHCDVLCDVJCD
    ZVNCDZWBVPNVBVHUTVHPDVAAUJQRVBVLVAVLPDUTBUJULRVBWEWFWJOWHIVIUKUMVBWEWGWKOWI
    IVMUKUMVHVLVJVNUNUOUHVBVCCDVQVGNABUPVCTUQUTVAVRVKVSVOEATBTURUS $.
    $( [14-Jul-2014] $) $( [31-Jul-1999] $)

  $( Complex conjugate distributes over multiplication.  Proposition 10-3.4(c)
     of [Gleason] p. 133.  (The proof was shortened by Mario Carneiro,
     14-Jul-2014.) $)
  cjmul $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) ) $=
    ( cc wcel wa cmul co cre cfv cim cmin wceq caddc ccj remullem simp3d ) ACDB
    CDEABFGZHIAHIZBHIZFGAJIZBJIZFGKGLQJIRUAFGTSFGMGLQNIANIBNIFGLABOP $.
    $( [14-Jul-2014] $) $( [29-Jul-1999] $)

  $( Standard inner product on complex numbers. $)
  ipcnval $p |- ( ( A e. CC /\ B e. CC ) -> ( Re ` ( A x. ( * ` B ) ) ) =
           ( ( ( Re ` A ) x. ( Re ` B ) ) + ( ( Im ` A ) x. ( Im ` B ) ) ) ) $=
    ( cc wcel wa cfv cmul co cre cmin cneg wceq adantl oveq2d imcl recnd syl2an
    cim recl mulcl ccj caddc cjcl remul sylan2 recj imcj mulneg2 oveq12d subneg
    eqtrd syl2anc 3eqtrd ) ACDZBCDZEZABUAFZGHIFZAIFZUQIFZGHZARFZUQRFZGHZJHZUSBI
    FZGHZVBBRFZGHZKZJHZVGVIUBHZUOUNUQCDURVELBUCAUQUDUEUPVAVGVDVJJUPUTVFUSGUOUTV
    FLUNBUFMNUPVDVBVHKZGHZVJUPVCVMVBGUOVCVMLUNBUGMNUNVBCDZVHCDZVNVJLUOUNVBAOPZU
    OVHBOPZVBVHUHQUKUIUPVGCDZVICDZVKVLLUNUSCDVFCDVSUOUNUSASPUOVFBSPUSVFTQUNVOVP
    VTUOVQVRVBVHTQVGVIUJULUM $.
    $( [14-Jul-2014] $) $( [29-Jul-1999] $)

  $( A complex number times its conjugate is real. $)
  cjmulrcl $p |- ( A e. CC -> ( A x. ( * ` A ) ) e. RR ) $=
    ( cc wcel ccj cfv cmul co cr wceq cjcj oveq2d cjmul mpdan mulcom 3eqtr4d wb
    cjcl mulcl cjreb syl mpbird ) ABCZAADEZFGZHCZUDDEZUDIZUBUCUCDEZFGZUCAFGZUFU
    DUBUHAUCFAJKUBUCBCZUFUIIAQZAUCLMUBUKUDUJIULAUCNMOUBUDBCZUEUGPUBUKUMULAUCRMU
    DSTUA $.
    $( [14-Jul-2014] $) $( [26-Mar-2005] $)

  $( A complex number times its conjugate. $)
  cjmulval $p |- ( A e. CC -> ( A x. ( * ` A ) ) =
                 ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) ) $=
    ( cc wcel cre cfv c2 cexp cim caddc cmul ccj wceq recl recnd sqval syl imcl
    co oveq12d ipcnval anidms cr cjmulrcl rere 3eqtr2rd ) ABCZADEZFGRZAHEZFGRZI
    RUGUGJRZUIUIJRZIRZAAKEJRZDEZUNUFUHUKUJULIUFUGBCUHUKLUFUGAMNUGOPUFUIBCUJULLU
    FUIAQNUIOPSUFUOUMLAATUAUFUNUBCUOUNLAUCUNUDPUE $.
    $( [14-Jul-2014] $) $( [1-Feb-2007] $)

  $( A complex number times its conjugate is nonnegative. $)
  cjmulge0 $p |- ( A e. CC -> 0 <_ ( A x. ( * ` A ) ) ) $=
    ( cc wcel cc0 cre cfv c2 cexp co cim caddc ccj cmul cle wbr recl resqcl syl
    cr sqge0 imcl addge0 syl22anc cjmulval breqtrrd ) ABCZDAEFZGHIZAJFZGHIZKIZA
    ALFMINUFUHSCZUJSCZDUHNOZDUJNOZDUKNOUFUGSCZULAPZUGQRUFUISCZUMAUAZUIQRUFUPUNU
    QUGTRUFURUOUSUITRUHUJUBUCAUDUE $.
    $( [14-Jul-2014] $) $( [26-Mar-2005] $)

  $( Complex conjugate of negative. $)
  cjneg $p |- ( A e. CC -> ( * ` -u A ) = -u ( * ` A ) ) $=
    ( cc wcel cneg cre cfv ci cim cmul cmin ccj wceq recl recnd sylancr syl2anc
    co ax-icn 3eqtr4d remim imcl mulcl neg2sub reneg imneg oveq2d mulneg2 eqtrd
    oveq12d negsubdi2 negcl syl negeqd ) ABCZADZEFZGUOHFZIQZJQZAEFZGAHFZIQZJQZD
    ZUOKFZAKFZDUNUTDZVBDZJQZVBUTJQZUSVDUNUTBCZVBBCZVIVJLUNUTAMNZUNGBCZVABCZVLRU
    NVAAUANZGVAUBOZUTVBUCPUNUPVGURVHJAUDUNURGVADZIQZVHUNUQVRGIAUEUFUNVNVOVSVHLR
    VPGVAUGOUHUIUNVKVLVDVJLVMVQUTVBUJPSUNUOBCVEUSLAUKUOTULUNVFVCATUMS $.
    $( [14-Jul-2014] $) $( [27-Feb-2005] $)

  $( A number plus its conjugate is twice its real part.  Compare Proposition
     10-3.4(h) of [Gleason] p. 133. $)
  addcj $p |- ( A e. CC -> ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) ) $=
    ( cc wcel c2 cre cfv cmul ccj caddc cdiv reval oveq2d wceq cjcl addcl mpdan
    co cc0 wne 2cn 2ne0 divcan2 mp3an23 syl eqtr2d ) ABCZDAEFZGQDAAHFZIQZDJQZGQ
    ZUIUFUGUJDGAKLUFUIBCZUKUIMZUFUHBCULANAUHOPULDBCDRSUMTUAUIDUBUCUDUE $.
    $( [14-Jul-2014] $) $( [21-Jan-2007] $)

  $( Complex conjugate distributes over subtraction. $)
  cjsub $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A - B ) ) = ( ( * ` A ) - ( * ` B ) ) ) $=
    ( cc wcel wa cneg caddc ccj cfv cmin negcl cjadd sylan2 negsub fveq2d cjneg
    co wceq adantl cjcl oveq2d syl2an eqtrd 3eqtr3d ) ACDZBCDZEZABFZGQZHIZAHIZU
    HHIZGQZABJQZHIUKBHIZJQZUFUEUHCDUJUMRBKAUHLMUGUIUNHABNOUGUMUKUOFZGQZUPUGULUQ
    UKGUFULUQRUEBPSUAUEUKCDUOCDURUPRUFATBTUKUONUBUCUD $.
    $( [1-Aug-2006] $) $( [28-Apr-2005] $)

  ${
    $d j k A $.  $d j k N $.
    $( Complex conjugate of natural number exponentiation. $)
    cjexp $p |- ( ( A e. CC /\ N e. NN0 ) ->
               ( * ` ( A ^ N ) ) = ( ( * ` A ) ^ N ) ) $=
      ( vj vk cn0 wcel cc cexp co ccj cfv wi cc0 c1 oveq2 fveq2d eqeq12d imbi2d
      wceq cmul cv caddc exp0 cjcl cr 1re cjre ax-mp syl6eqr eqtr4d expp1 expcl
      syl wa simpl cjmul syl2anc eqtrd adantr oveq1 sylan sylan9eqr exp31 com12
      eqcomd a2d nn0ind impcom ) BEFAGFZABHIZJKZAJKZBHIZSZVIACUAZHIZJKZVLVOHIZS
      ZLVIAMHIZJKZVLMHIZSZLVIADUAZHIZJKZVLWDHIZSZLVIAWDNUBIZHIZJKZVLWIHIZSZLVIV
      NLCDBVOMSZVSWCVIWNVQWAVRWBWNVPVTJVOMAHOPVOMVLHOQRVOWDSZVSWHVIWOVQWFVRWGWO
      VPWEJVOWDAHOPVOWDVLHOQRVOWISZVSWMVIWPVQWKVRWLWPVPWJJVOWIAHOPVOWIVLHOQRVOB
      SZVSVNVIWQVQVKVRVMWQVPVJJVOBAHOPVOBVLHOQRVIWANJKZWBVIVTNJAUCPVIVLGFZWBWRS
      AUDZWSWBNWRVLUCNUEFWRNSUFNUGUHUIUMUJWDEFZVIWHWMVIXAWHWMLVIXAWHWMVIXAUNZWH
      UNWKWFVLTIZWLXBWKXCSWHXBWKWEATIZJKZXCXBWJXDJAWDUKPXBWEGFVIXEXCSAWDULVIXAU
      OWEAUPUQURUSWHXBXCWGVLTIZWLWFWGVLTUTXBWLXFVIWSXAWLXFSWTVLWDUKVAVEVBURVCVD
      VFVGVH $.
      $( [7-Jun-2006] $)
  $}

  $( The imaginary part of a number in terms of complex conjugate. $)
  imval2 $p |- ( A e. CC ->
    ( Im ` A ) = ( ( A - ( * ` A ) ) / ( 2 x. _i ) ) ) $=
    ( cc wcel cim cfv c2 ci cmul co cdiv ccj cmin wceq recnd 2cn ax-icn syl2anc
    syl caddc oveq1d imcl cc0 wne mulcli 2ne0 ine0 mulne0i divcan4 mp3an23 recl
    mulcl sylancr addcl subsub syl3anc replim remim oveq12d 2times mulcom mpan2
    cre mulass mp3an12 eqtrd pncan2 3eqtr4d 3eqtr4rd eqtr3d ) ABCZADEZFGHIZHIZV
    LJIZVKAAKEZLIZVLJIVJVKBCZVNVKMZVJVKAUANZVQVLBCZVLUBUCVRFGOPUDZFGOPUEUFUGVKV
    LUHUIRVJVMVPVLJVJAVBEZGVKHIZSIZWBWCLIZLIZWDWBLIZWCSIZVPVMVJWDBCZWBBCZWCBCZW
    FWHMVJWJWKWIVJWBAUJNZVJGBCZVQWKPVSGVKUKULZWBWCUMQWLWNWDWBWCUNUOVJAWDVOWELAU
    PAUQURVJFWCHIZWCWCSIZVMWHVJWKWOWPMWNWCUSRVJVQVMWOMVSVQVMVLVKHIZWOVQVTVMWQMW
    AVKVLUTVAFBCWMVQWQWOMOPFGVKVCVDVERVJWGWCWCSVJWJWKWGWCMWLWNWBWCVFQTVGVHTVI
    $.
    $( [30-Apr-2005] $)
  ${
    cjcji.1 $e |- A e. CC $.
    $( The conjugate of the conjugate is the original complex number.
       Proposition 10-3.4(e) of [Gleason] p. 133. $)
    cjcji $p |- ( * ` ( * ` A ) ) = A $=
      ( cc wcel ccj cfv wceq cjcj ax-mp ) ACDAEFEFAGBAHI $.
      $( [11-May-1999] $)

    $( A number is real iff its imaginary part is 0. $)
    reim0bi $p |- ( A e. RR <-> ( Im ` A ) = 0 ) $=
      ( cc wcel cr cim cfv cc0 wceq wb reim0b ax-mp ) ACDAEDAFGHIJBAKL $.
      $( [29-May-1999] $)

    $( A real number equals its real part.  Proposition 10-3.4(f) of [Gleason]
       p. 133. $)
    rerebi $p |- ( A e. RR <-> ( Re ` A ) = A ) $=
      ( cc wcel cr cre cfv wceq wb rereb ax-mp ) ACDAEDAFGAHIBAJK $.
      $( [24-Jul-2008] $) $( [27-Oct-1999] $)

    $( A number is real iff it equals its complex conjugate.  Proposition
       10-3.4(f) of [Gleason] p. 133. $)
    cjrebi $p |- ( A e. RR <-> ( * ` A ) = A ) $=
      ( cc wcel cr ccj cfv wceq wb cjreb ax-mp ) ACDAEDAFGAHIBAJK $.
      $( [11-Oct-1999] $)

    $( Real part of a complex conjugate. $)
    recji $p |- ( Re ` ( * ` A ) ) = ( Re ` A ) $=
      ( cc wcel ccj cfv cre wceq recj ax-mp ) ACDAEFGFAGFHBAIJ $.
      $( [2-Oct-1999] $)

    $( Imaginary part of a complex conjugate. $)
    imcji $p |- ( Im ` ( * ` A ) ) = -u ( Im ` A ) $=
      ( cc wcel ccj cfv cim cneg wceq imcj ax-mp ) ACDAEFGFAGFHIBAJK $.
      $( [2-Oct-1999] $)

    ${
      readdi.2 $e |- B e. CC $.
      $( Real part distributes over addition. $)
      readdi $p |- ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) $=
        ( cc wcel caddc co cre cfv wceq readd mp2an ) AEFBEFABGHIJAIJBIJGHKCDAB
        LM $.
        $( [28-Jul-1999] $)

      $( Imaginary part distributes over addition. $)
      imaddi $p |- ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) $=
        ( cc wcel caddc co cim cfv wceq imadd mp2an ) AEFBEFABGHIJAIJBIJGHKCDAB
        LM $.
        $( [28-Jul-1999] $)

      $( Real part of a product. $)
      remuli $p |- ( Re ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Re ` B ) ) - ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( cc wcel cmul co cre cfv cim cmin wceq remul mp2an ) AEFBEFABGHIJAIJBI
        JGHAKJBKJGHLHMCDABNO $.
        $( [28-Jul-1999] $)

      $( Imaginary part of a product. $)
      immuli $p |- ( Im ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Im ` B ) ) + ( ( Im ` A ) x. ( Re ` B ) ) ) $=
        ( cc wcel cmul co cim cfv cre caddc wceq immul mp2an ) AEFBEFABGHIJAKJB
        IJGHAIJBKJGHLHMCDABNO $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
         [Gleason] p. 133. $)
      cjaddi $p |- ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) $=
        ( cc wcel caddc co ccj cfv wceq cjadd mp2an ) AEFBEFABGHIJAIJBIJGHKCDAB
        LM $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over multiplication.  Proposition
         10-3.4(c) of [Gleason] p. 133. $)
      cjmuli $p |- ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) $=
        ( cc wcel cmul co ccj cfv wceq cjmul mp2an ) AEFBEFABGHIJAIJBIJGHKCDABL
        M $.
        $( [28-Jul-1999] $)

      $( Standard inner product on complex numbers. $)
      ipcni $p |- ( Re ` ( A x. ( * ` B ) ) ) =
             ( ( ( Re ` A ) x. ( Re ` B ) ) + ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( cc wcel ccj cfv cmul co cre cim caddc wceq ipcnval mp2an ) AEFBEFABGH
        IJKHAKHBKHIJALHBLHIJMJNCDABOP $.
        $( [2-Oct-1999] $)
    $}

    $( A complex number times its conjugate is real. $)
    cjmulrcli $p |- ( A x. ( * ` A ) ) e. RR $=
      ( cc wcel ccj cfv cmul co cr cjmulrcl ax-mp ) ACDAAEFGHIDBAJK $.
      $( [11-May-1999] $)

    $( A complex number times its conjugate. $)
    cjmulvali $p |- ( A x. ( * ` A ) ) =
                   ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) $=
      ( cc wcel ccj cfv cmul co cre c2 cexp cim caddc wceq cjmulval ax-mp ) ACD
      AAEFGHAIFJKHALFJKHMHNBAOP $.
      $( [2-Oct-1999] $)

    $( A complex number times its conjugate is nonnegative. $)
    cjmulge0i $p |- 0 <_ ( A x. ( * ` A ) ) $=
      ( cc wcel cc0 ccj cfv cmul co cle wbr cjmulge0 ax-mp ) ACDEAAFGHIJKBALM
      $.
      $( [28-May-1999] $)

    $( Real part of negative. $)
    renegi $p |- ( Re ` -u A ) = -u ( Re ` A ) $=
      ( cc wcel cneg cre cfv wceq reneg ax-mp ) ACDAEFGAFGEHBAIJ $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( The negative of a real is real. $)
    negrebi $p |- ( -u A e. RR <-> A e. RR ) $=
      ( cc wcel cneg cr wb negreb ax-mp ) ACDAEFDAFDGBAHI $.
      $( [11-Aug-1999] $)

    $( Imaginary part of negative. $)
    imnegi $p |- ( Im ` -u A ) = -u ( Im ` A ) $=
      ( cc wcel cneg cim cfv wceq imneg ax-mp ) ACDAEFGAFGEHBAIJ $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( Complex conjugate of negative. $)
    cjnegi $p |- ( * ` -u A ) = -u ( * ` A ) $=
      ( cc wcel cneg ccj cfv wceq cjneg ax-mp ) ACDAEFGAFGEHBAIJ $.
      $( [2-Aug-1999] $)

    $( A number plus its conjugate is twice its real part.  Compare Proposition
       10-3.4(h) of [Gleason] p. 133. $)
    addcji $p |- ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) $=
      ( cc wcel ccj cfv caddc co c2 cre cmul wceq addcj ax-mp ) ACDAAEFGHIAJFKH
      LBAMN $.
      $( [2-Oct-1999] $)
  $}

  $( The real part of zero. $)
  re0 $p |- ( Re ` 0 ) = 0 $=
    ( cc0 cr wcel cre cfv wceq 0re 0cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The imaginary part of zero. $)
  im0 $p |- ( Im ` 0 ) = 0 $=
    ( cc0 cr wcel cim cfv wceq 0re 0cn reim0bi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The real part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  re1 $p |- ( Re ` 1 ) = 1 $=
    ( c1 cr wcel cre cfv wceq 1re ax-1cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [9-Jun-2006] $)

  $( The imaginary part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  im1 $p |- ( Im ` 1 ) = 0 $=
    ( c1 cr wcel cim cfv cc0 wceq 1re ax-1cn reim0bi mpbi ) ABCADEFGHAIJK $.
    $( [9-Jun-2006] $)

  $( The real part of ` _i ` .  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  rei $p |- ( Re ` _i ) = 0 $=
    ( cc0 ci c1 cmul co caddc cre cfv ax-icn ax-1cn mulcli addid2i fveq2i crrei
    0re 1re mulid1i 3eqtr3ri ) ABCDEZFEZGHSGHABGHTSGSBCIJKLMACOPNSBGBIQMR $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The imaginary part of ` _i ` .  (Contributed by Scott Fenton,
     9-Jun-2006.) $)
  imi $p |- ( Im ` _i ) = 1 $=
    ( ci c1 cmul cim cfv cc0 ax-icn ax-1cn mulcli addid2i eqcomi fveq2i mulid1i
    co caddc 0re 1re crimi 3eqtr3i ) ABCNZDEFTONZDEADEBTUADUATTABGHIJKLTADAGMLF
    BPQRS $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The conjugate of zero. $)
  cj0 $p |- ( * ` 0 ) = 0 $=
    ( cc0 cr wcel ccj cfv wceq 0re 0cn cjrebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The complex conjugate of the imaginary unit. $)
  cji $p |- ( * ` _i ) = -u _i $=
    ( ci cre cfv cim cmul co cmin cc0 ccj cneg rei c1 imi oveq2i ax-icn mulid1i
    eqtri oveq12i cc wcel wceq remim ax-mp df-neg 3eqtr4i ) ABCZAADCZEFZGFZHAGF
    AICZAJUFHUHAGKUHALEFAUGLAEMNAOPQRASTUJUIUAOAUBUCAUDUE $.
    $( [26-Mar-2005] $)

  $( The conjugate of a representation of a complex number in terms of real and
     imaginary parts. $)
  cjreim $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A + ( _i x. B ) ) ) =
                ( A - ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul co caddc ccj cfv cneg cc wceq recn ax-icn sylancr cjre
    syl2an 3eqtrd cmin mulcl cjadd cjmul cji oveq12d mulneg1 oveqan12d negsub
    a1i ) ACDZBCDZEAFBGHZIHJKZAJKZUMJKZIHZAUMLZIHZAUMUAHZUKAMDZUMMDZUNUQNULAOZU
    LFMDZBMDZVBPBOZFBUBQZAUMUCSUKULUOAUPURIARULUPFJKZBJKZGHZFLZBGHZURULVDVEUPVJ
    NPVFFBUDQULVHVKVIBGVHVKNULUEUJBRUFULVDVEVLURNPVFFBUGQTUHUKVAVBUSUTNULVCVGAU
    MUIST $.
    $( [1-Aug-2006] $) $( [1-Jul-2005] $)

  $( The conjugate of the representation of a complex number in terms of real
     and imaginary parts. $)
  cjreim2 $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A - ( _i x. B ) ) ) =
                ( A + ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul cmin ccj cfv cneg wceq recn ax-icn sylancr syl2an cjre
    co cc 3eqtrd caddc mulcl cjsub cjmul cji oveq12d mulneg1 oveqan12d subneg
    a1i ) ACDZBCDZEAFBGRZHRIJZAIJZUMIJZHRZAUMKZHRZAUMUARZUKASDZUMSDZUNUQLULAMZU
    LFSDZBSDZVBNBMZFBUBOZAUMUCPUKULUOAUPURHAQULUPFIJZBIJZGRZFKZBGRZURULVDVEUPVJ
    LNVFFBUDOULVHVKVIBGVHVKLULUEUJBQUFULVDVEVLURLNVFFBUGOTUHUKVAVBUSUTLULVCVGAU
    MUIPT $.
    $( [1-Jul-2005] $)

  $( Complex conjugate is a one-to-one function.  (Proof shortened by Eric
     Schmidt, 2-Jul-2009.) $)
  cj11 $p |- ( ( A e. CC /\ B e. CC ) ->
              ( ( * ` A ) = ( * ` B ) <-> A = B ) ) $=
    ( cc wcel wa ccj cfv wceq fveq2 cjcj eqeqan12d syl5ib impbid1 ) ACDZBCDZEZA
    FGZBFGZHZABHZSQFGZRFGZHPTQRFINOUAAUBBAJBJKLABFIM $.
    $( [2-Jul-2009] $) $( [29-Apr-2005] $)

  $( A number is nonzero iff its complex conjugate is nonzero. $)
  cjne0 $p |- ( A e. CC -> ( A =/= 0 <-> ( * ` A ) =/= 0 ) ) $=
    ( cc wcel cc0 ccj cfv wceq cj0 eqeq2i wb 0cn cj11 mpan2 syl5rbbr necon3bid
    ) ABCZADAEFZDQDGQDEFZGZPADGZRDQHIPDBCSTJKADLMNO $.
    $( [29-Apr-2005] $)

  ${
    cjdiv.1 $e |- A e. CC $.
    cjdiv.2 $e |- B e. CC $.
    $( Complex conjugate distributes over division. $)
    cjdivi $p |- ( B =/= 0 ->
                    ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
      ( cc0 wne ccj cfv cdiv co wceq cmul cc divclzi cjmul sylancr divcan2zi wb
      wcel cjcli fveq2d eqtr3d cjcl syl 0cn cj11 mp2an cj0 eqeq2i bitr3i biimpi
      necon3bii wa divmul mp3an1 mpanr1 syl2anc mpbird eqcomd ) BEFZAGHZBGHZIJZ
      ABIJZGHZUTVCVEKZVBVELJZVAKZUTBVDLJZGHZVGVAUTBMSZVDMSZVJVGKDABCDNZBVDOPUTV
      IAGABCDQUAUBUTVEMSZVBEFZVFVHRZUTVLVNVMVDUCUDUTVOBEVBEBEKZVBEGHZKZVBEKVKEM
      SVSVQRDUEBEUFUGVREVBUHUIUJULUKVNVBMSZVOVPBDTVAMSVNVTVOUMVPACTVAVEVBUNUOUP
      UQURUS $.
      $( [6-Sep-2013] $) $( [29-Apr-2005] $)
  $}

  $( Complex conjugate distributes over division. $)
  cjdiv $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                   ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
    ( cc wcel cc0 wne cdiv co ccj cfv wceq wi oveq1 fveq2d fveq2 oveq1d eqeq12d
    cif 0cn elimel imbi2d neeq1 oveq2 oveq2d imbi12d cjdivi dedth2h 3impia ) AC
    DZBCDZBEFZABGHZIJZAIJZBIJZGHZKZUIUJUKUQLUKUIAERZBGHZIJZURIJZUOGHZKZLUJBERZE
    FZURVDGHZIJZVAVDIJZGHZKZLABEEAURKZUQVCUKVKUMUTUPVBVKULUSIAURBGMNVKUNVAUOGAU
    RIOPQUABVDKZUKVEVCVJBVDEUBVLUTVGVBVIVLUSVFIBVDURGUCNVLUOVHVAGBVDIOUDQUEURVD
    AECSTBECSTUFUGUH $.
    $( [3-Feb-2006] $) $( [29-Apr-2005] $)

  ${
    $d F u v w z $.  $d x y u v w z $.
    cnrecnv.1 $e |- F = ( x e. RR , y e. RR |-> ( x + ( _i x. y ) ) ) $.
    $( The inverse to the canonical bijection from ` ( RR X. RR ) ` to ` CC `
       from ~ cnref1o .  (Contributed by Mario Carneiro, 25-Aug-2014.) $)
    cnrecnv $p |- `' F = ( z e. CC |-> <. ( Re ` z ) , ( Im ` z ) >. ) $=
      ( ccnv cc cv cfv cmpt cre wceq cr wf1o wcel ci cmul co caddc syl2anc f1of
      cim cop wfn cxp cnref1o f1ocnv ax-mp ffn mp2b dffn5v mpbi df-ov recl imcl
      wf oveq1 oveq2 oveq2d ovex ovmpt2 syl5eqr replim eqtr4d opelxpi f1ocnvfv1
      fveq2d sylancr eqtr3d mpteq2ia eqtri ) DFZCGCHZVLIZJZCGVMKIZVMUBIZUCZJVLG
      UDZVLVOLGMMUEZVLNZGVTVLUPVSVTGDNZWAABDEUFZVTGDUGUHGVTVLUAGVTVLUIUJCGVLUKU
      LCGVNVRVMGOZVRDIZVLIZVNVRWDWEVMVLWDWEVPPVQQRZSRZVMWDWEVPVQDRZWHVPVQDUMWDV
      PMOZVQMOZWIWHLVMUNZVMUOZABVPVQMMAHZPBHZQRZSRWHDVPWPSRWNVPWPSUQWOVQLWPWGVP
      SWOVQPQURUSEVPWGSUTVATVBVMVCVDVGWDWBVRVTOZWFVRLWCWDWJWKWQWLWMVPVQMMVETVTG
      VRDVFVHVIVJVK $.
      $( [25-Aug-2014] $)
  $}

