$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The "shift" operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c shift $.  $( Symbol for function shifter. $)

  $( Extend class notation with function shifter. $)
  cshi $a class shift $.

  ${
    $d x y z f g $.
    $( Define a function shifter.  This operation offsets the value argument of
       a function (ordinarily on a subset of ` CC ` ) and produces a new
       function on ` CC ` .  See ~ shftval for its value. $)
    df-shft $a |- shift = ( f e. _V , x e. CC |->
                  { <. y , z >. | ( y e. CC /\ ( y - x ) f z ) } ) $.
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Two ways to write a shifted set ` ( B + A ) ` . $)
    shftlem $p |- ( ( A e. CC /\ B C_ CC ) ->
          { x e. CC | ( x - A ) e. B } = { x | E. y e. B x = ( y + A ) } ) $=
      ( cc wcel wss wa cv cmin co crab cab caddc wceq wrex ancoms oveq1 sylan
      wi df-rab npcan eqcomd eqeq2d rcla4ev expcom expimpd ssel2 ax-addcl pncan
      syl adantr simplr eqeltrd jca anassrs eleq1d anbi12d syl5ibrcom rexlimdva
      eleq1 impbid abbidv syl5eq ) CEFZDEGZHZAIZCJKZDFZAELVHEFZVJHZAMVHBIZCNKZO
      ZBDPZAMVJAEUAVGVLVPAVGVLVPVEVLVPTVFVEVKVJVPVEVKHZVHVICNKZOZVJVPTVQVRVHVKV
      EVRVHOVHCUBQUCVJVSVPVOVSBVIDVMVIOVNVRVHVMVICNRUDUEUFUKUGULVGVOVLBDVGVMDFZ
      HVLVOVNEFZVNCJKZDFZHZVEVFVTWDVFVTHZVEWDWEVEHZWAWCWEVMEFZVEWADEVMUHZVMCUIS
      WFWBVMDWEWGVEWBVMOWHVMCUJSVFVTVEUMUNUOQUPVOVKWAVJWCVHVNEVAVOVIWBDVHVNCJRU
      QURUSUTVBVCVD $.
      $( [3-Nov-2013] $)

    $( A shift of the upper integers. $)
    shftuz $p |- ( ( A e. ZZ /\ B e. ZZ ) ->
          { x e. CC | ( x - A ) e. ( ZZ>= ` B ) } = ( ZZ>= ` ( B + A ) ) ) $=
      ( cz wcel wa cv cmin co cuz cfv cc crab cab caddc df-rab w3a zcn ancoms
      wi wceq simp2 3ad2ant1 syl2anc eluzadd 3adant2 eqeltrrd 3expib adantr syl
      npcan eluzelz a1i eluzsub 3expia jcad impbid abbi1dv syl5eq ) BDEZCDEZFZA
      GZBHIZCJKEZALMVCLEZVEFZANCBOIZJKZVEALPVBVGAVIVBVGVCVIEZUTVGVJTVAUTVFVEVJU
      TVFVEQZVDBOIZVCVIVKVFBLEZVLVCUAUTVFVEUBUTVFVMVEBRUCVCBUKUDUTVEVLVIEZVFVEU
      TVNBCVDUESUFUGUHUIVBVJVFVEVJVFTVBVJVCDEVFVHVCULVCRUJUMVAUTVJVETVAUTVJVEBC
      VCUNUOSUPUQURUS $.
      $( [5-Nov-2013] $)
  $}

  ${
    $d w x y z A $.  $d w x y z F $.  $d w x y z B $.
    shftfval.1 $e |- F e. _V $.
    $( The value of the sequence shifter operation is a function on ` CC ` .
       ` A ` is ordinarily an integer. $)
    shftfval $p |- ( A e. CC -> ( F shift A ) =
                    { <. x , y >. | ( x e. CC /\ ( x - A ) F y ) } ) $=
      ( vz vw cc wcel cv cmin co wbr wa copab cvv cshi wceq caddc wrex cdm ovex
      cab crn cxp breldm npcan eqcomd ancoms oveq1 eqeq2d rcla4ev syl2anr eqeq1
      wss rexbidv elab sylibr brelrn adantl expl ssopab2dv df-xp syl6sseqr dmex
      vex jca abrexex rnex xpex ssexg sylancl breq anbi2d opabbidv oveq2 breq1d
      df-shft ovmpt2g mp3an1 mpdan ) CHIZAJZHIZWCCKLZBJZDMZNZABOZPIZDCQLWIRZWBW
      IFJZGJZCSLZRZGDUAZTZFUCZDUDZUEZUOWTPIWJWBWIWCWRIZWFWSIZNZABOWTWBWHXCABWBW
      DWGXCWBWDNZWGNZXAXBXEWCWNRZGWPTZXAWGWEWPIWCWECSLZRZXGXDWEWFDWCCKUBZUFWDWB
      XIWDWBNXHWCWCCUGUHUIXFXIGWEWPWMWERWNXHWCWMWECSUJUKULUMWQXGFWCAVFWLWCRWOXF
      GWPWLWCWNUNUPUQURWGXBXDWEWFDXJBVFUSUTVGVAVBABWRWSVCVDWRWSGFWPWNDEVEVHDEVI
      VJWIWTPVKVLDPIWBWJWKEFGDCPHWDWCWMKLZWFWLMZNZABOWIQWDXKWFDMZNZABOPWLDRZXMX
      OABXPXLXNWDXKWFWLDVMVNVOWMCRZXOWHABXQXNWGWDXQXKWEWFDWMCWCKVPVQVNVOGABFVRV
      SVTWA $.
      $( [3-Nov-2013] $) $( [20-Jul-2005] $)

    $( Domain of a relation shifted by ` A ` .  The set on the right is more
       commonly notated as ` ( dom F + A ) ` (meaning add ` A ` to every
       element of ` dom F ` ). $)
    shftdm $p |- ( A e. CC -> dom ( F shift A ) =
                  { x e. CC | ( x - A ) e. dom F } ) $=
      ( vy cc wcel cshi co cdm cv cmin wbr wa copab crab shftfval dmeqd wex cab
      19.42v ovex eldm anbi2i bitr4i abbii dmopab df-rab 3eqtr4i syl6eq ) BFGZC
      BHIZJAKZFGZUMBLIZEKCMZNZAEOZJZUOCJGZAFPZUKULURAEBCDQRUQESZATUNUTNZATUSVAV
      BVCAVBUNUPESZNVCUNUPEUAUTVDUNEUOCUMBLUBUCUDUEUFUQAEUGUTAFUHUIUJ $.
      $( [3-Nov-2013] $)

    $( Value of a fiber of the relation ` F ` . $)
    shftfib $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift A ) " { B } ) = ( F " { ( B - A ) } ) ) $=
      ( vz vx vy cc wcel wa cv co wbr cab cmin csn cima cvv wb wceq copab breqd
      cshi shftfval eleq1 oveq1 breq1d anbi12d breq2 anbi2d eqid brabg sylan9bb
      vex mpan2 ibar adantl bitr4d abbidv imasng ovex ax-mp a1i 3eqtr4d ) AHIZB
      HIZJZBEKZCAUCLZMZENZBAOLZVHCMZENZVIBPQZCVLPQZVGVJVMEVGVJVFVMJZVMVEVJBVHFK
      ZHIZVRAOLZGKZCMZJZFGUAZMZVFVQVEVIWDBVHFGACDUDUBVFVHRIWEVQSEUNWCVFVLWACMZJ
      VQFGBVHHRWDVRBTZVSVFWBWFVRBHUEWGVTVLWACVRBAOUFUGUHWAVHTWFVMVFWAVHVLCUIUJW
      DUKULUOUMVFVMVQSVEVFVMUPUQURUSVFVOVKTVEEBHVIUTUQVPVNTZVGVLRIWHBAOVAEVLRCU
      TVBVCVD $.
      $( [4-Nov-2013] $)

    $( Functionality and domain of a sequence shifted by ` A ` . $)
    shftfn $p |- ( ( F Fn B /\ A e. CC ) ->
                  ( F shift A ) Fn { x e. CC | ( x - A ) e. B } ) $=
      ( vy vz vw wfn cc wcel wa co wfun cdm cv cmin wceq wbr wmo cshi crab wrel
      copab wal relopab a1i fnfun adantr wi ovex ax-17 breq1 mobid imbi2d funmo
      vtocl vex eleq1 oveq1 breq1d anbi12d breq2 anbi2d eqid brab simprbi immoi
      alrimiv dffun6 sylanbrc shftfval adantl funeqd mpbird shftdm fndm rabbidv
      syl eleq2d sylan9eqr df-fn ) DCIZBJKZLZDBUAMZNZWFOZAPZBQMZCKZAJUBZRWFWLIW
      EWGWIJKZWJFPZDSZLZAFUDZNZWEWQUCZGPZHPZWQSZHTZGUEZWRWSWEWPAFUFUGWEDNZXDWCX
      EWDCDUHUIXEXCGXEWTBQMZXADSZHTZXCXEWIXADSZHTZUJXEXHUJAXFWTBQUKWIXFRZXJXHXE
      XKXIXGHXKHULWIXFXADUMUNUOAHDUPUQXBXGHXBWTJKZXGWPXLXFWNDSZLXLXGLAFWTXAWQGU
      RHURWIWTRZWMXLWOXMWIWTJUSXNWJXFWNDWIWTBQUTVAVBWNXARXMXGXLWNXAXFDVCVDWQVEV
      FVGVHVSVIVSGHWQVJVKWEWFWQWDWFWQRWCAFBDEVLVMVNVOWDWCWHWJDOZKZAJUBWLABDEVPW
      CXPWKAJWCXOCWJCDVQVTVRWAWFWLWBVK $.
      $( [3-Nov-2013] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A ` . $)
    shftval $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift A ) ` B ) = ( F ` ( B - A ) ) ) $=
      ( vx cc wcel wa cshi co csn cima cv wceq cab cuni cmin cfv shftfib df-fv
      eqeq1d abbidv unieqd 3eqtr4g ) AFGBFGHZCAIJZBKLZEMKZNZEOZPCBAQJZKLZUHNZEO
      ZPBUFRUKCRUEUJUNUEUIUMEUEUGULUHABCDSUAUBUCEBUFTEUKCTUD $.
      $( [4-Nov-2013] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( F shift ( A - B ) ) ` ( A + C ) ) = ( F ` ( B + C ) ) ) $=
      ( cc wcel w3a caddc cmin cshi wceq subcl 3adant3 ax-addcl 3adant2 shftval
      co cfv syl2anc pnncan 3com23 addcom 3adant1 eqtr4d fveq2d eqtrd ) AFGZBFG
      ZCFGZHZACIRZDABJRZKRSZULUMJRZDSZBCIRZDSUKUMFGZULFGZUNUPLUHUIURUJABMNUHUJU
      SUIACOPUMULDEQTUKUOUQDUKUOCBIRZUQUHUJUIUOUTLACBUAUBUIUJUQUTLUHBCUCUDUEUFU
      G $.
      $( [5-Nov-2013] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval3 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift ( A - B ) ) ` A ) = ( F ` B ) ) $=
      ( cc wcel wa cc0 caddc co cmin cshi cfv 0cn shftval2 mp3an3 addid1 adantr
      wceq fveq2d adantl 3eqtr3d ) AEFZBEFZGZAHIJZCABKJLJZMZBHIJZCMZAUGMBCMUCUD
      HEFUHUJSNABHCDOPUEUFAUGUCUFASUDAQRTUEUIBCUDUIBSUCBQUATUB $.
      $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` -u A ` . $)
    shftval4 $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift -u A ) ` B ) = ( F ` ( A + B ) ) ) $=
      ( cc wcel wa cneg cshi co cfv cmin caddc wceq negcl shftval subneg ancoms
      sylan addcom eqtr4d fveq2d eqtrd ) AEFZBEFZGZBCAHZIJKZBUGLJZCKZABMJZCKUDU
      GEFUEUHUJNAOUGBCDPSUFUIUKCUFUIBAMJZUKUEUDUIULNBAQRABTUAUBUC $.
      $( [5-Nov-2013] $) $( [18-Aug-2005] $)

    $( Value of a shifted sequence. $)
    shftval5 $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift A ) ` ( B + A ) ) = ( F ` B ) ) $=
      ( cc wcel caddc co cshi wceq wa cmin simpr ax-addcl shftval syl2anc pncan
      cfv fveq2d eqtrd ancoms ) BEFZAEFZBAGHZCAIHRZBCRZJUBUCKZUEUDALHZCRZUFUGUC
      UDEFUEUIJUBUCMBANAUDCDOPUGUHBCBAQSTUA $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $d y C $.
    $( Functionality of a shifted sequence. $)
    shftf $p |- ( ( F : B --> C /\ A e. CC ) ->
          ( F shift A ) : { x e. CC | ( x - A ) e. B } --> C ) $=
      ( vy wf cc wcel wa co cv cmin wfn cfv wceq simpr simpl syl2an cshi shftfn
      crab wral ffn sylan oveq1 elrab shftval ffvelrn eqeltrd sylan2b ralrimiva
      eleq1d ffnfv sylanbrc ) CDEHZBIJZKZEBUALZAMZBNLZCJZAIUCZOZGMZUTPZDJZGVDUD
      VDDUTHUQECOURVECDEUEABCEFUBUFUSVHGVDVFVDJUSVFIJZVFBNLZCJZKZVHVCVKAVFIVAVF
      QVBVJCVAVFBNUGUNUHUSVLKVGVJEPZDUSURVIVGVMQVLUQURRVIVKSBVFEFUITUSUQVKVMDJV
      LUQURSVIVKRCDVJEUJTUKULUMGVDDUTUOUP $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $( Composite shift operations. $)
    2shfti $p |- ( ( A e. CC /\ B e. CC ) -> ( ( F shift A ) shift B ) =
                ( F shift ( A + B ) ) ) $=
      ( vx vy vz vw cc wcel wa cv cmin co cshi wbr copab wb shftfval wceq caddc
      breqd ovex vex eleq1 oveq1 breq1d anbi12d breq2 anbi2d eqid brab ad2antrr
      syl6bb subcl biantrurd ancoms adantll sub32 subsub4 eqtr3d 3expb pm5.32da
      w3a 3bitr2d opabbidv adantl ax-addcl syl 3eqtr4d ) AIJZBIJZKZELZIJZVNBMNZ
      FLZCAONZPZKZEFQZVOVNABUANZMNZVQCPZKZEFQZVRBONZCWBONZVMVTWEEFVMVOVSWDVMVOK
      ZVSVPIJZVPAMNZVQCPZKZWLWDVKVSWMRVLVOVKVSVPVQGLZIJZWNAMNZHLZCPZKZGHQZPWMVK
      VRWTVPVQGHACDSUBWSWJWKWQCPZKWMGHVPVQWTVNBMUCFUDWNVPTZWOWJWRXAWNVPIUEXBWPW
      KWQCWNVPAMUFUGUHWQVQTXAWLWJWQVQWKCUIUJWTUKULUNUMVLVOWLWMRZVKVOVLXCVOVLKWJ
      WLVNBUOUPUQURWIWKWCVQCVOVMWKWCTZVOVKVLXDVOVKVLVDVNAMNBMNWKWCVNABUSVNABUTV
      AVBUQUGVEVCVFVLWGWATVKEFBVRCAOUCSVGVMWBIJWHWFTABVHEFWBCDSVIVJ $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $( Identity law for the shift operation. $)
    shftidt2 $p |- ( F shift 0 ) = ( F |` CC ) $=
      ( vx vy cv cc wcel cc0 cmin co wbr copab cshi cres subid1 pm5.32i opabbii
      wa breq1d wceq 0cn shftfval ax-mp dfres2 3eqtr4i ) CEZFGZUFHIJZDEZAKZRZCD
      LZUGUFUIAKZRZCDLAHMJZAFNUKUNCDUGUJUMUGUHUFUIAUFOSPQHFGUOULTUACDHABUBUCCDF
      AUDUE $.
      $( [5-Nov-2013] $)

    $( Identity law for the shift operation. $)
    shftidt $p |- ( A e. CC -> ( ( F shift 0 ) ` A ) = ( F ` A ) ) $=
      ( cc wcel cc0 cshi co cfv cres shftidt2 fveq1i fvres syl5eq ) ADEABFGHZIA
      BDJZIABIAOPBCKLADBMN $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan1 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift A ) shift -u A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel cshi co cneg cfv cc0 caddc wceq negcl 2shfti mpdan negid oveq2d
      eqtrd fveq1d shftidt sylan9eq ) AEFZBEFBCAGHAIZGHZJBCKGHZJBCJUCBUEUFUCUEC
      AUDLHZGHZUFUCUDEFUEUHMANAUDCDOPUCUGKCGAQRSTBCDUAUB $.
      $( [5-Nov-2013] $) $( [4-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan2 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift -u A ) shift A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel wa cneg cshi co wceq negneg adantr oveq2d fveq1d negcl shftcan1
      cfv sylan eqtr3d ) AEFZBEFZGZBCAHZIJZUDHZIJZRZBUEAIJZRBCRZUCBUGUIUCUFAUEI
      UAUFAKUBALMNOUAUDEFUBUHUJKAPUDBCDQST $.
      $( [5-Nov-2013] $) $( [4-Aug-2005] $)
  $}

  ${
    $d F x y z $.  $d M x y z $.  $d N x y z $.  $d P x y z $.
    seqshft.1 $e |- F e. _V $.
    $( Shifting the index set of a sequence.  (Contributed by Mario Carneiro,
       2-Jul-2013.) $)
    seqshft $p |- ( ( M e. ZZ /\ N e. ZZ ) -> seq M ( P , ( F shift N ) ) =
        ( seq ( M - N ) ( P , F ) shift N ) ) $=
      ( vx vy cz wcel wa cuz cfv co cmin wfn cc zcn caddc wceq syl2an cshi cseq
      vz seqfn adantr cv crab zsubcl syl adantl seqex shftfn simpr shftuz npcan
      syl2anc fveq2d eqtrd fneq2d negsub seqeq1d eluzelz syl2anr fveq12d znegcl
      mpbid cneg ad2antlr wral elfzelz shftval ancoms eqtr4d ralrimiva r19.21bi
      cfz seqshft2 uzssz zsscn sstri sseli 3eqtr4d eqfnfvd ) CHIZDHIZJZUCCKLZAB
      DUAMZCUBZABCDNMZUBZDUAMZWDWIWGOWEAWHCUDUEWFWLFUFDNMWJKLZIFPUGZOZWLWGOWFWK
      WMOZDPIZWOWFWJHIZWPCDUHZABWJUDUIWEWQWDDQZUJZFDWMWKABWJUKZULUPWFWNWGWLWFWN
      WJDRMZKLZWGWFWEWRWNXDSWDWEUMWSFDWJUNUPWFXCCKWDCPIZWQXCCSWECQZWTCDUOTUQURU
      SVFWFUCUFZWGIZJZXGDVGZRMZABCXJRMZUBZLXGDNMZWKLZXGWILXGWLLZXIXKXNXMWKXIXLW
      JABWFXLWJSZXHWDXEWQXQWEXFWTCDUTTUEVAXHXGPIZWQXKXNSWFXHXGHIXRCXGVBXGQUIXAX
      GDUTVCVDXIAGWHBXJCXGWFXHUMWEXJHIWDXHDVEVHXIGUFZWHLZXSXJRMZBLZSZGCXGVPMZWE
      YCGYDVIWDXHWEYCGYDWEWQXSPIZYCXSYDIZWTYFXSHIYEXSCXGVJXSQUIWQYEJZXTXSDNMZBL
      YBDXSBEVKYGYAYHBYEWQYAYHSXSDUTVLUQVMTVNVHVOVQWFWQXRXPXOSXHXAWGPXGWGHPCVRV
      SVTWADXGWKXBVKTWBWC $.
      $( [27-Feb-2014] $) $( [17-Mar-2005] $)
  $}

