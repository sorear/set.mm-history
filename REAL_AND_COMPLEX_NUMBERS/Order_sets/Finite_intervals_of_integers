$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Finite intervals of integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ... $. $( Dots. $)

  $( Extend class notation to include the notation for a contiguous finite set
     of integers.  Read " ` M ... N ` " as "the set of integers from ` M ` to
     ` N ` inclusive." $)
  cfz $a class ... $.

  ${
    $d m n k z $.
    $( Define an operation that produces a finite set of sequential integers.
       Read " ` M ... N ` " as "the set of integers from ` M ` to ` N `
       inclusive."  See ~ fzval for its value and additional comments. $)
    df-fz $a |- ... = ( m e. ZZ , n e. ZZ |->
                  { k e. ZZ | ( m <_ k /\ k <_ n ) } ) $.
  $}

  ${
    $d k m n z M $.  $d k m n z N $.
    $( The value of a finite set of sequential integers.  E.g., ` 2 ... 5 `
       means the set ` { 2 , 3 , 4 , 5 } ` .  A special case of this definition
       (starting at 1) appears as Definition 11-2.1 of [Gleason] p. 141, where
       ` NN ` _k means our ` 1 ... k ` ; he calls these sets _segments_ of the
       integers.  (Revised by Mario Carneiro, 3-Nov-2013.) $)
    fzval $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M ... N ) =
                 { k e. ZZ | ( M <_ k /\ k <_ N ) } ) $=
      ( vm vn cz cv cle wbr wa crab cfz breq1 anbi1d rabbidv breq2 anbi2d df-fz
      wceq zex rabex ovmpt2 ) DEBCFFDGZAGZHIZUDEGZHIZJZAFKBUDHIZUDCHIZJZAFKLUIU
      GJZAFKUCBSZUHULAFUMUEUIUGUCBUDHMNOUFCSZULUKAFUNUGUJUIUFCUDHPQOADERUKAFTUA
      UB $.
      $( [3-Nov-2013] $) $( [6-Sep-2005] $)

    $( An alternative way of expressing a finite set of sequential integers.
       (Contributed by Mario Carneiro, 3-Nov-2013.) $)
    fzval2 $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
        ( M ... N ) = ( ( M [,] N ) i^i ZZ ) ) $=
      ( vk cz wcel wa cfz co cv cle wbr crab cicc cin fzval wceq cr zssre sseli
      cxr ressxr sstri iccval syl2an ineq1d inrab2 wss sseqin2 mpbi rabeq ax-mp
      eqtri syl6req eqtrd ) ADEZBDEZFZABGHACIZJKURBJKFZCDLZABMHZDNZCABOUQVBUSCT
      LZDNZUTUQVAVCDUOATEBTEVAVCPUPDTADQTRUAUBZSDTBVESCABUCUDUEVDUSCTDNZLZUTUSC
      TDUFVFDPZVGUTPDTUGVHVEDTUHUIUSCVFDUJUKULUMUN $.
      $( [3-Nov-2013] $)

    $( Establish the domain and codomain of the finite integer sequence
       function.  (Contributed by Scott Fenton, 8-Aug-2013.)  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    fzf $p |- ... : ( ZZ X. ZZ ) --> ~P ZZ $=
      ( vm vk vn cv cle wbr wa cz crab cpw wcel cxp cfz wf wss ssrab2 zex elpw2
      wral mpbir rgen2w df-fz fmpt2 mpbi ) ADBDZEFUECDEFGZBHIZHJZKZCHSAHSHHLUHM
      NUIACHHUIUGHOUFBHPUGHQRTUAACHHUGUHMBACUBUCUD $.
      $( [16-Nov-2013] $) $( [8-Aug-2013] $)
  $}

  ${
    $d j K $.  $d j M $.  $d j N $.
    $( Membership in a finite set of sequential integers. $)
    elfz1 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... N ) <->
                 ( K e. ZZ /\ M <_ K /\ K <_ N ) ) ) $=
      ( vj cz wcel wa cfz co cv cle wbr crab w3a fzval wceq breq2 breq1 anbi12d
      eleq2d elrab 3anass bitr4i syl6bb ) BEFCEFGZABCHIZFABDJZKLZUGCKLZGZDEMZFZ
      AEFZBAKLZACKLZNZUEUFUKADBCOTULUMUNUOGZGUPUJUQDAEUGAPUHUNUIUOUGABKQUGACKRS
      UAUMUNUOUBUCUD $.
      $( [21-Jul-2005] $)
  $}

  $( Membership in a finite set of sequential integers. $)
  elfz $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... N ) <->
                 ( M <_ K /\ K <_ N ) ) ) $=
    ( cz wcel cfz co cle wbr wa wb w3a elfz1 3anass baib sylan9bb 3impa 3comr )
    BDEZCDEZADEZABCFGEZBAHIZACHIZJZKZSTUAUFSTJUBUAUCUDLZUAUEABCMUGUAUEUAUCUDNOP
    QR $.
    $( [29-Sep-2005] $)

  ${
    $d k n m z M $.  $d k n m z N $.
    $( Membership in a finite set of sequential integers.  We use the fact that
       an operation's value is empty outside of its domain to show ` M e. ZZ `
       and ` N e. ZZ ` . $)
    elfz2 $p |- ( N e. A -> ( K e. ( M ... N ) <->
           ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) ) ) ) $=
      ( wcel cfz co cz wa cle wbr w3a wb elfz1 3anass ibar bitrd adantl c0 wceq
      syl5bb wn cdm cxp cpw fzf fdmi ndmovg mp3an1 eleq2d noel pm2.21i pm5.21ni
      simpl pm2.61dan df-3an anbi1i anass bitr2i syl6bb ) DAEZBCDFGZEZCHEZDHEZI
      ZBHEZCBJKZBDJKZIZIZIZVDVEVGLZVJIZVAVFVCVLMZVFVOVAVFVCVGVHVILZVLBCDNVPVKVF
      VLVGVHVIOVFVKPUAQRVAVFUBZIZVCBSEZVLVRVBSBFUCHHUDZTVAVQVBSTVTHUEFUFUGCDAHH
      FUHUIUJVQVSVLMVAVSVFVLVSVFBUKULVFVKUNUMRQUOVNVFVGIZVJIVLVMWAVJVDVEVGUPUQV
      FVGVJURUSUT $.
      $( [3-Nov-2013] $) $( [6-Sep-2005] $)

    $( Lemma for ~ elfzel1 and others. $)
    elfzlem $p |- ( K e. ( M ... N ) ->
                    ( ( M e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) ) ) $=
      ( cvv wcel cfz co cz wa cle wbr wi w3a elfz2 3simpb syl6bi wn wrel anbi2d
      pm2.61i anim1i ovprc2 eleq2d anim12i cr zre leid syl 3ad2ant3 adantr jcad
      simpl anass syl6ib c0 wceq n0i cdm cxp relxp cpw fdmi releqi mpbir ovprc1
      fzf pm2.24d syl5 brprc syl5ibr sylbid ) CDEZABCFGZEZBHEZAHEZIZBAJKZACJKZI
      ZIZLVLVNVOCHEZVPMZVTIWADABCNWCVQVTVOWBVPOUAPVLQZVNABBFGZEZWAWDVMWEABCFUBU
      CWFWAWDVQVRAAJKZIZIZBDEZWFWILWJWFVQVRIZWGIWIWJWFWKWGWJWFVOVOVPMZVRABJKZIZ
      IZWKDABBNZWLVQWNVRVOVOVPOVRWMULUDPWJWFWOWGWPWLWGWNVPVOWGVOVPAUEEWGAUFAUGU
      HUIUJPUKVQVRWGUMUNWFWEUOUPZQWJQZWIWEAUQWRWQWIBBFFURZRHHUSZRHHUTWSWTWTHVAF
      VFVBVCVDVEVGVHTWDVTWHVQWDVSWGVRACJVISSVJVKT $.
      $( [3-Nov-2013] $) $( [6-Sep-2005] $)
  $}

  $( Membership in a finite set of sequential integers. $)
  elfz5 $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. ZZ ) ->
                 ( K e. ( M ... N ) <-> K <_ N ) ) $=
    ( cuz cfv wcel cz wa cfz co cle wbr wb eluzelz eluzel2 jca elfz 3expa sylan
    eluzle biantrurd adantr bitr4d ) ABDEFZCGFZHABCIJFZBAKLZACKLZHZUHUDAGFZBGFZ
    HUEUFUIMZUDUJUKBANBAOPUJUKUEULABCQRSUDUHUIMUEUDUGUHBATUAUBUC $.
    $( [26-Dec-2005] $)

  $( Membership in a finite set of sequential integers. $)
  elfz4 $p |- ( ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) )
               -> K e. ( M ... N ) ) $=
    ( cz wcel w3a cle wbr cfz co wi wa elfz1 biimprcd 3exp com4r 3impia imp32 )
    BDEZCDEZADEZFBAGHZACGHZABCIJEZSTUAUBUCUDKKUAUBUCSTLZUDUAUBUCUEUDKUEUDUAUBUC
    FABCMNOPQR $.
    $( [21-Jul-2005] $)

  $( Membership in a finite set of sequential integers in terms of sets of
     upper integers. $)
  elfzuzb $p |- ( N e. A -> ( K e. ( M ... N ) <->
         ( K e. ( ZZ>= ` M ) /\ N e. ( ZZ>= ` K ) ) ) ) $=
    ( wcel cfz co cz w3a cle wbr cuz cfv elfz2 an6 eluz2 anbi12i df-3an anandir
    wa ancom anbi2i 3bitri anbi1i bitr4i 3bitr4ri syl6bb ) DAEBCDFGECHEZDHEZBHE
    ZIZCBJKZBDJKZTZTZBCLMEZDBLMEZTZABCDNUHUJULIZUJUIUMIZTUHUJTZUJUITZUNIZURUOUH
    UJULUJUIUMOUPUSUQUTCBPBDPQUOVAVBTZUNTVCUKVDUNUKUHUITUJTVAUIUJTZTVDUHUIUJRUH
    UIUJSVEVBVAUIUJUAUBUCUDVAVBUNRUEUFUG $.
    $( [18-Sep-2005] $)

  $( Membership in a finite set of sequential integers. $)
  eluzfz $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. ( ZZ>= ` K ) )
               -> K e. ( M ... N ) ) $=
    ( cz wcel cuz cfv wa cfz co eluzelz adantl elfzuzb biimpar mpancom ) CDEZAB
    FGEZCAFGEZHZABCIJEZRPQACKLPTSDABCMNO $.
    $( [4-Oct-2005] $)

  $( Membership in a finite set of sequential integers implies membership in a
     set of upper integers. $)
  elfzuz3 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ( ZZ>= ` K ) ) $=
    ( wcel cfz co cuz cfv elfzuzb simplbda ) DAEBCDFGEBCHIEDBHIEABCDJK $.
    $( [28-Sep-2005] $)

  ${
    elfzel2.1 $e |- N e. _V $.
    $( Membership in a finite set of sequential integer implies the upper bound
       is an integer. $)
    elfzel2i $p |- ( K e. ( M ... N ) -> N e. ZZ ) $=
      ( cfz co wcel cz w3a cle wbr wa cvv wb elfz2 ax-mp simpl2 sylbi ) ABCEFGZ
      BHGZCHGZAHGZIBAJKACJKLZLZUACMGSUDNDMABCOPTUAUBUCQR $.
      $( [6-Sep-2005] $)
  $}

  $( Membership in a finite set of sequential integers implies the upper bound
     is an integer. $)
  elfzel2g $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ZZ ) $=
    ( wcel cfz co cz w3a cle wbr wa elfz2 simpl2 syl6bi imp ) DAEZBCDFGEZDHEZQR
    CHEZSBHEZICBJKBDJKLZLSABCDMTSUAUBNOP $.
    $( [6-Sep-2005] $)

  $( Membership in a finite set of sequential integer implies the lower bound
     is an integer. $)
  elfzel1 $p |- ( K e. ( M ... N ) -> M e. ZZ ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simpll syl ) ABCDEFBGFZAGFZHBAIJACIJHZH
    NABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is an integer. $)
  elfzelz $p |- ( K e. ( M ... N ) -> K e. ZZ ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simplr syl ) ABCDEFBGFZAGFZHBAIJACIJHZH
    OABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is greater than or equal to
     the lower bound. $)
  elfzle1 $p |- ( K e. ( M ... N ) -> M <_ K ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simprl syl ) ABCDEFBGFAGFHZBAIJZACIJZHH
    OABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is less than or equal to
     the upper bound. $)
  elfzle2 $p |- ( K e. ( M ... N ) -> K <_ N ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simprr syl ) ABCDEFBGFAGFHZBAIJZACIJZHH
    PABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( Membership in a finite set of sequential integer implies the bounds are
     comparable. $)
  elfzle3 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> M <_ N ) $=
    ( wcel cfz co wa cle wbr elfzle1 adantl elfzle2 cz elfzel1 elfzelz elfzel2g
    wi cr zre letr syl3an syl3anc mp2and ) DAEZBCDFGEZHZCBIJZBDIJZCDIJZUFUHUEBC
    DKLUFUIUEBCDMLUGCNEZBNEZDNEZUHUIHUJRZUFUKUEBCDOLUFULUEBCDPLABCDQUKCSEULBSEU
    MDSEUNCTBTDTCBDUAUBUCUD $.
    $( [18-Sep-2005] $)

  $( Implication of membership in a finite set of sequential integers. $)
  elfzuz2 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ( ZZ>= ` M ) ) $=
    ( wcel cfz co wa cuz cfv elfzuzb ancom syl6bb biimpa uztrn syl ) DAEZBCDFGE
    ZHDBIJEZBCIJZEZHZDTEQRUBQRUASHUBABCDKUASLMNBDCOP $.
    $( [20-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz1 $p |- ( N e. ( ZZ>= ` M ) -> M e. ( M ... N ) ) $=
    ( cuz cfv wcel cz cle wbr w3a cfz co eluz2 simp1 simp2 cr zre leid 3ad2ant1
    syl simp3 elfz4 syl32anc sylbi ) BACDEAFEZBFEZABGHZIZAABJKEZABLUGUDUEUDAAGH
    ZUFUHUDUEUFMZUDUEUFNUJUDUEUIUFUDAOEUIAPAQSRUDUEUFTAABUAUBUC $.
    $( [21-Jul-2005] $)

  $( A member of a finite set of sequential integers belongs to a set of upper
     integers. $)
  elfzuz $p |- ( K e. ( M ... N ) -> K e. ( ZZ>= ` M ) ) $=
    ( cfz co wcel cz cle wbr cuz cfv elfzel1 elfzelz elfzle1 eluz2 syl3anbrc )
    ABCDEFBGFAGFBAHIABJKFABCLABCMABCNBAOP $.
    $( [17-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz2 $p |- ( N e. ( ZZ>= ` M ) -> N e. ( M ... N ) ) $=
    ( cuz cfv wcel cz cle wbr cfz co eluzel2 eluzelz eluzle zre leid 3syl elfz4
    cr syl32anc ) BACDEZAFEBFEZUAABGHBBGHZBABIJEABKABLZUCABMTUABREUBUCBNBOPBABQ
    S $.
    $( [13-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz2b $p |- ( N e. ( ZZ>= ` M ) <-> N e. ( M ... N ) ) $=
    ( cuz cfv wcel cfz co eluzfz2 elfzuz impbii ) BACDEBABFGEABHBABIJ $.
    $( [14-Sep-2005] $)

  $( Membership in a finite set of sequential integers containing one
     integer. $)
  elfz3 $p |- ( N e. ZZ -> N e. ( N ... N ) ) $=
    ( cz wcel cuz cfv cfz co uzid eluzfz1 syl ) ABCAADECAAAFGCAHAAIJ $.
    $( [21-Jul-2005] $)

  $( Membership in a finite set of sequential integers containing one
     integer. $)
  elfz1eq $p |- ( K e. ( N ... N ) -> K = N ) $=
    ( cfz co wcel cz wa cle wbr wceq elfzlem cr wb letri3 syl2an biimpar eqcomd
    zre syl ) ABBCDEBFEZAFEZGZBAHIABHIGZGZABJABBKUDBAUBBAJZUCTBLEALEUEUCMUABRAR
    BANOPQS $.
    $( [19-Sep-2005] $)

  $( A Peano-postulate-like theorem for downward closure of a finite set of
     sequential integers.  (Contributed by Mario Carneiro, 27-May-2014.) $)
  peano2fzr $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. V /\ ( K + 1 ) e. ( M ... N ) )
                    -> K e. ( M ... N ) ) $=
    ( cuz cfv wcel c1 caddc co cfz w3a wa wb elfzuzb 3ad2ant2 simp1 eluzelz syl
    cz elfzuz3 3adant1 peano2uzr syl2anc mpbir2and ) ABEFGZCDGZAHIJZBCKJZGZLZAU
    IGZUFCAEFGZUGUFULUFUMMNUJDABCOPUFUGUJQZUKATGZCUHEFGZUMUKUFUOUNBARSUGUJUPUFD
    UHBCUAUBACUCUDUE $.
    $( [27-May-2014] $)

  ${
    $d K k m $.  $d M k m $.  $d N k m $.
    $( A finite set of sequential integers is empty if the bounds are
       reversed. $)
    fzn $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( N < M <-> ( M ... N ) = (/) ) ) $=
      ( vk cz wcel wa clt wbr cfz co c0 wceq cle wn cr wb zre lenlt syl2an w3a
      cuz cfv eluz2 eluzfz1 sylbir n0i syl 3expia wi cv neq0 elfzle3 ex exlimdv
      wex syl5bi adantl impbid bitr3d con4bid ) ADEZBDEZFZBAGHZABIJZKLZVCABMHZV
      DNZVFNZVAAOEBOEVGVHPVBAQBQABRSVCVGVIVAVBVGVIVAVBVGTZAVEEZVIVJBAUAUBEVKABU
      CABUDUEVEAUFUGUHVBVIVGUIVAVICUJZVEEZCUOVBVGCVEUKVBVMVGCVBVMVGDVLABULUMUNU
      PUQURUSUT $.
      $( [18-Apr-2006] $) $( [22-Aug-2005] $)

    $( A shifted finite set of sequential integers is equinumerous to the
       original set.  (Contributed by Paul Chapman, 11-Apr-2009.) $)
    fzen $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) ->
                 ( M ... N ) ~~ ( ( M + K ) ... ( N + K ) ) ) $=
      ( cz wcel w3a co caddc cle wbr wi wa elfz1 biimpd adantrd zre syl3an wceq
      cr wb vk vm cfz cv cmin cvv ovex a1i zaddcl expcom 3ad2ant3 leadd1 3com23
      3adant3 3expia imp3a 3adant2 adantld 3coml 3adant1 3jcad syld com12 3impb
      syl2anc biimprd zsubcl leaddsub lesubadd ancoms simp1d ex cc subadd eqcom
      imp zcn 3bitr3g addcom eqeq2d bitrd 3expib syl2and en3d ) BDEZCDEZADEZFZU
      AUBBCUCGZBAHGZCAHGZUCGZUAUDZAHGZUBUDZAUEGZWIUFEWHBCUCUGUHWHWMWIEZWMDEZBWM
      IJZWMCIJZFZWNWLEZWEWFWQXAKWGWEWFLZWQXAWMBCMNUNZXAWHXBWRWSWTWHXBKWHWRWSWTL
      ZLZXBWHXFWNDEZWJWNIJZWNWKIJZFZXBWHXFXGXHXIWHWRXGXEWGWEWRXGKWFWRWGXGWMAUIU
      JUKOWEWGXFXHKWFWEWGLZWRXEXHWEWGWRXEXHKZWEWRWGXLWEWRWGFZWSXHWTXMWSXHWEBSEZ
      WRWMSEZWGASEZWSXHTBPZWMPZAPZBWMAULQNOUMUOUPUQWFWGXFXIKWEWFWGLWRXEXIWFWGWR
      XEXIKZWRWFWGXTWRWFWGFZWTXIWSYAWTXIWRXOWFCSEZWGXPWTXITXRCPZXSWMCAULQNURUSU
      OUPUTVAWHXBXJWHWJDEZWKDEZXBXJTWEWGYDWFBAUIUQZWFWGYEWECAUIUTZWNWJWKMVEVFVB
      VCVDVCVBWHWOWLEZWODEZWJWOIJZWOWKIJZFZWPWIEZWHYHYLWHYDYEYHYLTYFYGWOWJWKMVE
      NZYLWHYMYIYJYKWHYMKWHYIYJYKLZLZYMWHYPWPDEZBWPIJZWPCIJZFZYMWHYPYQYRYSWHYIY
      QYOWGWEYIYQKWFYIWGYQWOAVGUJUKOWEWGYPYRKWFXKYIYOYRWEWGYIYOYRKWEWGYIFZYJYRY
      KUUAYJYRWEXNWGXPYIWOSEZYJYRTXQXSWOPZBAWOVHQNOUOUPUQWFWGYPYSKZWEWGWFUUDWGW
      FLYIYOYSWGWFYIYOYSKZYIWGWFUUEYIWGWFFZYKYSYJUUFYSYKYIUUBWGXPWFYBYSYKTUUCXS
      YCWOACVIQVFURUSUOUPVJUTVAWEWFYTYMKWGXCYMYTWPBCMVFUNVBVCVDVCVBWHWQWRYHYIWM
      WPRZWOWNRZTZWHWQWRWHWQLWRWSWTWHWQXAXDVPVKVLWHYHYIWHYHLYIYJYKWHYHYLYNVPVKV
      LWGWEWRYILUUIKWFWGWRYIUUIYIWGWRUUIYIWOVMEZWGAVMEZWRWMVMEZUUIWOVQAVQWMVQUU
      JUUKUULFZUUGWOAWMHGZRZUUHUUMWPWMRUUNWORUUGUUOWOAWMVNWPWMVOUUNWOVOVRUUMUUN
      WNWOUUKUULUUNWNRUUJAWMVSUTVTWAQUSWBUKWCWD $.
      $( [11-Apr-2009] $)
  $}

  $( A 1-based finite set of sequential integers is empty iff it ends at index
     ` 0 ` .  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  fz1n $p |- ( N e. NN0 -> ( ( 1 ... N ) = (/) <-> N = 0 ) ) $=
    ( cn0 wcel c1 clt wbr cfz co c0 wceq cc0 cz wb 1z nn0z fzn sylancr nn0lt10b
    bitr3d ) ABCZADEFZDAGHIJZAKJTDLCALCUAUBMNAODAPQARS $.
    $( [22-Jun-2011] $)

  $( Two ways to say a finite 1-based sequence is empty.  (Contributed by Paul
     Chapman, 26-Oct-2012.) $)
  0fz1 $p |- ( ( N e. NN0 /\ F Fn ( 1 ... N ) ) -> ( F = (/) <-> N = 0 ) ) $=
    ( c1 cfz co wfn c0 wceq cn0 wcel cc0 fndmu sylan2br ex fneq2 syl6bb biimpcd
    fn0 impbid fz1n sylan9bbr ) ACBDEZFZAGHZUBGHZBIJBKHUCUDUEUCUDUEUDUCAGFZUEAR
    ZUBGALMNUEUCUDUEUCUFUDUBGAOUGPQSBTUA $.
    $( [26-Oct-2012] $)

  $( There are no integers between 1 and 0.  (Contributed by Jeff Madsen,
     16-Jun-2010.) $)
  fz10 $p |- ( 1 ... 0 ) = (/) $=
    ( vx c1 cc0 cfz co c0 wceq cv wcel eq0 cle wbr clt lt01 0re 1re ltnlei mpbi
    wn cr elfzle3 mpan mto mpgbir ) BCDEZFGAHZUEIZSAAUEJUGBCKLZCBMLUHSNCBOPQRCT
    IUGUHOTUFBCUAUBUCUD $.
    $( [16-Jun-2010] $)

  ${
    $d M x $.  $d N x $.  $d A x $.
    $( Properties of a finite interval of integers which is non-empty.
       (Contributed by Jeff Madsen, 17-Jun-2010.) $)
    fzn0 $p |- ( ( N e. A /\ ( M ... N ) =/= (/) ) ->
                                      ( M e. ZZ /\ N e. ZZ /\ M <_ N ) ) $=
      ( vx cfz co c0 wne wcel cv wex cz cle wbr w3a n0 elfzel1 adantl elfzel2g
      wa elfzle3 3jca ex exlimdv imp sylan2b ) BCEFZGHCAIZDJZUGIZDKZBLIZCLIZBCM
      NZOZDUGPUHUKUOUHUJUODUHUJUOUHUJTULUMUNUJULUHUIBCQRAUIBCSAUIBCUAUBUCUDUEUF
      $.
      $( [17-Jun-2010] $)
  $}

  ${
    $d M x $.  $d N x $.  $d K x $.
    $( Split a finite interval of integers into two parts.  (Contributed by
       Jeff Madsen, 17-Jun-2010.) $)
    fzsplit $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> ( M ... N )
                              = ( ( M ... K ) u. ( ( K + 1 ) ... N ) ) ) $=
      ( wcel cfz co cz cle wbr w3a sylan2 wa wi wo cr zre 3anass wb elfz1 vx c1
      caddc cun wceq c0 wne ne0i fzn0 elfzelz elfzle1 elfzle2 3jca an6 lelttric
      cv clt syl2anr zltp1le orbi2d 3ad2antl2 adantlr biantrud andi letr syl3an
      ancoms ancomsd expdimp ancom2s 3adantl1 anassrs adantlrl adantld pm4.71rd
      mpbid 3expb anasss ancom anbi1i anass bitri syl6rbbr anim12i letrp1 3expa
      peano2re exp3a 3expia com23 imp sylan syl5 impr 3adantl3 adantlrr adantrd
      syldan anbi2i orbi12d syl5bb bitrd pm5.32da orbi12i bitr4i 3bitr4g adantr
      3adant2 3adant3 peano2z 3adant1 eqrdv exp32 imp42 3impa 3adant3l sylancom
      elun 3bitr4d sylbi ) DAEZBCDFGZEZCHEZDHEZCDIJZKZYBCBFGZBUBUCGZDFGZUDZUEZY
      CYAYBUFUGYGYBBUHACDUILYCYGBHEZCBIJZBDIJZKZYLYCYMYNYOBCDUJBCDUKBCDULUMYGYP
      MYDYMMZYEYNMZYFYOMKYLYDYEYFYMYNYOUNYQYRYOYLYFYQYRYOYLYQYEYNYOYLYDYMYEYNYO
      YLNNYDYMYEKZYNYOYLYSYNYOMZMZUAYBYKUUAUAUPZHEZCUUBIJZUUBDIJZKZUUCUUDUUBBIJ
      ZKZUUCYIUUBIJZUUEKZOZUUBYBEZUUBYKEZUUAUUCUUDUUEMZMUUCUUDUUGMZUUIUUEMZOZMZ
      UUFUUKUUAUUCUUNUUQUUAUUCMZUUNUUNUUGUUIOZMZUUQUUSUUTUUNYSUUCUUTYTYMYDUUCUU
      TYEYMUUCMZUUGBUUBUQJZOZUUTUUCUUBPEZBPEZUVDYMUUBQZBQZUUBBUOURUVBUVCUUIUUGB
      UUBUSUTVPVAVBVCUVAUUNUUGMZUUNUUIMZOUUSUUQUUNUUGUUIVDUUSUVIUUOUVJUUPUUSUUO
      UUEUUOMZUVIUUSUUOUUEUUSUUGUUEUUDYSYOUUCUUGUUENZYNYSYOUUCUVLYMYEYOUUCMUVLY
      DYMYEMZUUCYOUVLUVMUUCYOUVLUVMUUCMZYOUUGUUEUVNUUGYOUUEUUCUVMUUGYOMUUENZUUC
      YMYEUVOUUCUVEYMUVFYEDPEUVOUVGUVHDQUUBBDVEVFVQVGVHVIVRVJVKVLVMVNVOUVIUUEUU
      DMZUUGMUVKUUNUVPUUGUUDUUEVSVTUUEUUDUUGWAWBWCUUSUUPUUDUUPMZUVJUUSUUPUUDUUS
      UUIUUDUUEYSYNUUCUUIUUDNZYOYSYNUUCUVRYDYMYNUUCMUVRYEYQYNUUCUVRUUCUVEYQYNMU
      VRUVGYQCPEZUVFMZYNUVEUVRNZYDUVSYMUVFCQUVHWDUVTYNCYIIJZUWAUVSUVFYNUWBCBWEW
      FUVTUWBUWAUVFUVSYIPEZUWBUWANBWGUVSUWCMUVEUWBUVRUVSUWCUVEUWBUVRNUVSUWCUVEK
      UWBUUIUUDCYIUUBVEWHWIWJLWKWRWLWMWNWOVLWPWQVOUVJUUDUUEUUIMZMUVQUUDUUEUUIWA
      UWDUUPUUDUUEUUIVSWSWBWCWTXAXBXCUUCUUDUUERUUKUUCUUOMZUUCUUPMZOUURUUHUWEUUJ
      UWFUUCUUDUUGRUUCUUIUUERXDUUCUUOUUPVDXEXFYSUULUUFSZYTYDYEUWGYMUUBCDTXHXGYS
      UUMUUKSYTUUMUUBYHEZUUBYJEZOYSUUKUUBYHYJXRYSUWHUUHUWIUUJYDYMUWHUUHSYEUUBCB
      TXIYMYEUWIUUJSZYDYMYIHEYEUWJBXJUUBYIDTWLXKWTXAXGXSXLXMWIXNXOXPXTLXQ $.
      $( [17-Jun-2010] $)

    $d J x $.  $d A x $.
    $( Condition for two finite intervals of integers to be disjoint.
       (Contributed by Jeff Madsen, 17-Jun-2010.) $)
    fzdisj $p |- ( ( K e. A /\ K < M ) ->
                                ( ( J ... K ) i^i ( M ... N ) ) = (/) ) $=
      ( vx wcel wbr wa cfz co wn cr cle cz zre syl wi ancom2s imp elin elfzel2g
      clt cin elfzle2 adantl jca elfzel1 elfzle1 elfzelz jca31 letr 3expb an12s
      cv anasss lenlt ancoms ad2antrl mpbid expcom an4s impr syl2an expl syl5bi
      wb con2d eq0rdv ) CAGZCDUCHZIFBCJKZDEJKZUDZVJVKFUOZVNGZLVJVPVKVPVOVLGZVOV
      MGZIVJVKLZVOVLVMUAVJVQVRVSVJVQIZCMGZVOCNHZIZDMGZDVONHZIZVOMGZIVSVRVTWAWBV
      TCOGWAAVOBCUBCPQVQWBVJVOBCUEUFUGVRWDWEWGVRDOGWDVODEUHDPQVODEUIVRVOOGWGVOD
      EUJVOPQUKWCWFWGVSWAWDWBWEWGVSRWGWAWDIZWBWEIZIZVSWGWJIDCNHZVSWGWHWIWKWGWHI
      ZWEWBWKWLWEWBIZWKWGWDWAWMWKRZWDWGWAWNWDWGWAWNDVOCULUMUNSTSUPWHWKVSVGZWGWI
      WDWAWODCUQURUSUTVAVBVCVDVEVFVHTVI $.
      $( [17-Jun-2010] $)
  $}

  $( 0-based and 1-based finite sets of sequential integers are equinumerous.
     (Contributed by Paul Chapman, 11-Apr-2009.) $)
  fz01en $p |- ( N e. ZZ -> ( 0 ... ( N - 1 ) ) ~~ ( 1 ... N ) ) $=
    ( cz wcel cc0 c1 cmin co cfz caddc cen wbr peano2zm 0z 1z fzen mp3an13 wceq
    syl ax-1cn cc addid2i a1i zcn npcan sylancl oveq12d breqtrd ) ABCZDAEFGZHGZ
    DEIGZUIEIGZHGZEAHGJUHUIBCZUJUMJKZALDBCUNEBCUOMNEDUIOPRUHUKEULAHUKEQUHESUAUB
    UHATCETCULAQAUCSAEUDUEUFUG $.
    $( [11-Apr-2009] $)

  $( A member of a finite set of sequential integers starting at 1 is a natural
     number. $)
  elfznn $p |- ( K e. ( 1 ... N ) -> K e. NN ) $=
    ( c1 cfz co wcel cz cle wbr cn elfzelz elfzle1 elnnz1 sylanbrc ) ACBDEFAGFC
    AHIAJFACBKACBLAMN $.
    $( [24-Aug-2005] $)

  $( Membership in a finite set of sequential integers starting at 0. $)
  elfz2nn0 $p |- ( N e. A ->
           ( K e. ( 0 ... N ) <-> ( K e. NN0 /\ N e. NN0 /\ K <_ N ) ) ) $=
    ( wcel cc0 cfz co cz w3a cle wbr wa cn0 elfz2 simpr simpl cr zre jca elnn0z
    anim12i simpll wi 0re letr mp3an1 syl2anr simprr 3jca 3adantl1 0z 3anim123i
    imp a1i 3com13 id adantll 3adant2 impbii biid 3anbi123i bitr4i syl6bb ) CAD
    BECFGDEHDZCHDZBHDZIZEBJKZBCJKZLZLZBMDZCMDZVIIZABECNVKVFVHLZVEECJKZLZVIIZVNV
    KVRVEVFVJVRVDVEVFLZVJLZVOVQVIVSVFVJVHVEVFOVHVIPUAVTVEVPVEVFVJUBVSVJVPVFBQDZ
    CQDZVJVPUCZVEBRCREQDWAWBWCUDEBCUEUFUGUMSVSVHVIUHUIUJVRVGVJVIVQVOVGVIVDVQVEV
    OVFVDVIUKUNVEVPPVFVHPULUOVOVIVJVQVHVIVJVFVJUPUQURSUSVLVOVMVQVIVIBTCTVIUTVAV
    BVC $.
    $( [16-Sep-2005] $)

  $( A member of a finite set of sequential integers starting at 0 is a
     nonnegative integer. $)
  elfznn0 $p |- ( K e. ( 0 ... N ) -> K e. NN0 ) $=
    ( cc0 cfz co wcel cz cle wbr cn0 elfzelz elfzle1 elnn0z sylanbrc ) ACBDEFAG
    FCAHIAJFACBKACBLAMN $.
    $( [5-Aug-2005] $)

  $( The upper bound of a nonempty finite set of sequential integers starting
     at 0 is a nonnegative integer. $)
  elfz3nn0 $p |- ( ( N e. A /\ K e. ( 0 ... N ) ) -> N e. NN0 ) $=
    ( wcel cc0 cfz co cn0 cle wbr w3a elfz2nn0 simp2 syl6bi imp ) CADZBECFGDZCH
    DZPQBHDZRBCIJZKRABCLSRTMNO $.
    $( [16-Sep-2005] $)

  $( Subtraction closure for a member of a finite set of sequential
     integers. $)
  fznn0sub $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> ( N - K ) e. NN0 ) $=
    ( wcel cfz co cmin cn0 cz w3a cle wa elfz2 znn0sub2 3com12 adantrl 3adantl1
    wbr 3expa syl6bi imp ) DAEZBCDFGEZDBHGIEZUCUDCJEZDJEZBJEZKCBLSZBDLSZMZMUEAB
    CDNUGUHUKUEUFUGUHMUJUEUIUGUHUJUEUHUGUJUEBDOPTQRUAUB $.
    $( [16-Sep-2005] $)

  $( Subtraction closure for a member of a finite set of sequential
     integers. $)
  fznn0sub2 $p |- ( ( N e. A /\ K e. ( 0 ... N ) ) ->
                   ( N - K ) e. ( 0 ... N ) ) $=
    ( wcel cc0 cfz co cn0 cle wbr w3a wa fznn0sub elfz3nn0 elfzle1 adantl cz cr
    cmin zre elfzel2g elfzelz subge02 syl2an syl2anc mpbid 3jca elfz2nn0 syldan
    wb biimpar ) CADZBECFGZDZCBSGZHDZCHDZUOCIJZKZUOUMDZULUNLZUPUQURABECMABCNVAE
    BIJZURUNVBULBECOPVACQDZBQDZVBURUJZABECUAUNVDULBECUBPVCCRDBRDVEVDCTBTCBUCUDU
    EUFUGULUTUSAUOCUHUKUI $.
    $( [26-Sep-2005] $)

  $( Membership of a sum in a finite set of sequential integers. $)
  fzaddel $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( J e. ( M ... N ) <-> ( J + K ) e. ( ( M + K ) ... ( N + K ) ) ) ) $=
    ( cz wcel wa cle wbr w3a caddc co cfz wb zaddcl cr zre leadd1 syl3an 3expb
    simpl adantl adantlr 3com12 adantll 3anbi123d elfz1 adantr anandirs adantrl
    2thd syl2an 3bitr4d ) CEFZDEFZGZAEFZBEFZGZGZUQCAHIZADHIZJZABKLZEFZCBKLZVDHI
    ZVDDBKLZHIZJZACDMLFZVDVFVHMLFZUTUQVEVAVGVBVIUSUQVENUPUSUQVEUQURUAABOUKUBUNU
    SVAVGNZUOUNUQURVMUNCPFUQAPFZURBPFZVMCQAQZBQZCABRSTUCUOUSVBVINZUNUOUQURVRUQU
    OURVRUQVNUODPFURVOVRVPDQVQADBRSUDTUEUFUPVKVCNUSACDUGUHUPURVLVJNZUQUNUOURVSU
    NURGVFEFVHEFVSUOURGCBODBOVDVFVHUGULUIUJUM $.
    $( [30-Jul-2005] $)

  $( Membership of a difference in a finite set of sequential integers. $)
  fzsubel $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( J e. ( M ... N ) <-> ( J - K ) e. ( ( M - K ) ... ( N - K ) ) ) ) $=
    ( cz wcel wa cfz co cneg caddc cmin wb znegcl fzaddel cc zcn anim12i negsub
    wceq sylanr2 adantl oveqan12d anandirs adantrl eleq12d syl2an bitrd ) CEFZD
    EFZGZAEFZBEFZGZGACDHIFZABJZKIZCUPKIZDUPKIZHIZFZABLIZCBLIZDBLIZHIZFZUMUKULUP
    EFUOVAMBNAUPCDOUAUKCPFZDPFZGZAPFZBPFZGZVAVFMUNUIVGUJVHCQDQRULVJUMVKAQBQRVIV
    LGUQVBUTVEVLUQVBTVIABSUBVIVKUTVETZVJVGVHVKVMVGVKGVHVKGURVCUSVDHCBSDBSUCUDUE
    UFUGUH $.
    $( [1-Aug-2006] $) $( [30-Jul-2005] $)

  $( A finite set of sequential integers can represent an ordered pair. $)
  fzopth $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. A ) ->
                ( ( M ... N ) = ( J ... K ) <-> ( M = J /\ N = K ) ) ) $=
    ( cuz cfv wcel wa cfz co wceq eluzfz2 eleq2 syl5ib syl6com wi cle wbr cr wb
    anim1d elfzuz2 ancoms eluzfz1 syl5ibr elfzle1 adantl adantr jcad cz eluzel2
    zre letri3 syl2an sylibrd elfzle2 anim12ii eluzelre ex com23 oveq12 impbid1
    syl mpdd ) EDFGHZCAHZIZDEJKZBCJKZLZDBLZECLZIZVHVKCBFGHZVNVKVHEVJHZVGIVOVKVF
    VPVGVFEVIHVKVPDEMVIVJENOZUBVGVPVOAEBCUCUDPVFVKVOVNQQVGVFVOVKVNVFVOVKVNQVFVO
    IZVKVLVMVRVKDBRSZBDRSZIZVLVRVKVSVTVOVKVSQVFVKVOBVIHZVSVOWBVKBVJHBCUEVIVJBNU
    FBDEUGPUHVFVKVTQVOVKVFDVJHZVTVFDVIHVKWCDEUEVIVJDNODBCUGPUIUJVFDTHZBTHZVLWAU
    AVOVFDUKHWDDEULDUMVDVOBUKHWEBCULBUMVDDBUNUOUPVRVKECRSZCERSZIZVMVFVKWFVOWGVK
    VFVPWFVQEBCUQPVKVOCVIHZWGVOWIVKCVJHBCMVIVJCNUFCDEUQPURVFETHCTHVMWHUAVODEUSB
    CUSECUNUOUPUJUTVAUIVEDBECJVBVC $.
    $( [31-Oct-2005] $)

$(
  @{
    @d j K @.  @d j M @.  @d j N @.
    @( Union of two adjacent finite sets of sequential integers. @)
    fzunt @p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\ ( K <_ M /\ M <_ N ) )
                 -> ( ( K ... M ) u. ( M ... N ) ) = ( K ... N ) ) @=
      ( vj cz wcel w3a cle wbr wa cfz co cun cv wo cr wb wi letr exp3a com23
      3exp com3l imp42 adantlll adantrl anim2d 3expa imp adantlrl adantrr
      anim1d jaod impbid exp42 com34 ex 3imp zre syl3anOLD syl5OLD pm5.32d andi
      bicomi syl5bbOLD elfz1 3anass syl6bb 3adant3 adantr 3adant1 orbi12d
      3adant2 3bitr4d elun eqrdv ) AEFZBEFZCEFZGZABHIZBCHIZJZJZDABKLZBCKLZMZACK
      LZWDDNZWEFZWIWFFZOZWIWHFZWIWGFWDWIEFZAWIHIZWIBHIZJZJZWNBWIHIZWICHIZJZJZOZ
      WNWOWTJZJZWLWMWDWNWQXAOZJZXEXCWDWNXFXDWDWIPFZXFXDQZWNVTWCXHXIRZAPFZBPFZCP
      FZWCXJRZVQVRVSXKXLXMXNXKXLXMXNRXKXLJZXMXHWCXIXOXMXHWCXIXOXMXHJZJZWCJZXFXD
      XRWQXDXAXRWPWTWOXQWBWPWTRZWAXLXPWBXSXKXLXMXHWBXSXHXLXMWBXSRZXHXLXMXTXHXLX
      MGZWPWBWTYAWPWBWTWIBCSTUAUBUCUDUEUFUGXRWSWOWTXQWAWSWORZWBXOXHWAYBXMXOXHJZ
      WAYBYCWAWSWOXKXLXHWAWSJWORABWISUHTUIUJUKULUM?UNUOUPUQURAUSBUSCUSUTUIWIUSV
      AVBXGXCWNWQXAVCVDVEWDWJWRWKXBVTWJWRQZWCVQVRYDVSVQVRJWJWNWOWPGWRWIABVFWNWO
      WPVGVHVIVJVTWKXBQZWCVRVSYEVQVRVSJWKWNWSWTGXBWIBCVFWNWSWTVGVHVKVJVLVTWMXEQ
      ZWCVQVSYFVRVQVSJWMWNWOWTGXEWIACVFWNWOWTVGVHVMVJVNWIWEWFVOVEVP @.
      @( [ ?] @) @( [21-Jul-2005] @)
  @}
$)

  ${
    $d k M $.  $d k N $.  $d k K $.  $d k V $.
    $( Subset relationship for finite sets of sequential integers.  (Revised by
       Mario Carneiro, 30-Jun-2013.) $)
    fzss1 $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. V ) ->
                  ( K ... N ) C_ ( M ... N ) ) $=
      ( vk wcel wa cfz co cz cle wbr crab adantr cr zre sylan wceq fzval com12
      cuz cfv cv elfzel2g expcom wss eluzle wi eluzel2 eluzelz jca syl3an 3expa
      letr mpand anim1d ss2rabdv 3sstr4d sseld sylan2d ssrdv ) ABUAUBFZCDFZGZEA
      CHIZBCHIZEUCZVEFZVDVGVFFZVHVCCJFZVBVIVCVHVJDVGACUDUEVBVJGZVHVIVKVEVFVGVKA
      VGKLZVGCKLZGZEJMZBVGKLZVMGZEJMZVEVFVBVOVRUFVJVBVNVQEJVBVGJFZGZVLVPVMVTBAK
      LZVLVPVBWAVSBAUGNVBBJFZAJFZGVSWAVLGVPUHZVBWBWCBAUIZBAUJZUKWBWCVSWDWBBOFWC
      AOFVSVGOFWDBPAPVGPBAVGUNULUMQUOUPUQNVBWCVJVEVORWFEACSQVBWBVJVFVRRWEEBCSQU
      RUSTUTTVA $.
      $( [30-Jun-2013] $) $( [28-Sep-2005] $)

    $( Subset relationship for finite sets of sequential integers.  (Revised by
       Mario Carneiro, 30-Jun-2013.) $)
    fzss2 $p |- ( N e. ( ZZ>= ` K ) -> ( M ... K ) C_ ( M ... N ) ) $=
      ( vk cuz wcel cfz co cz wi wa cle wbr crab adantl zre sylan2 wceq fzval
      cr cfv cv elfzel1 wss eluzle eluzel2 eluzelz jca letr syl3an 3expb mpan2d
      ancoms anim2d ss2rabdv 3sstr4d sseld expcom com23 mpdi ssrdv ) CAEUAFZDBA
      GHZBCGHZVBDUBZVCFZBIFZVEVDFZVEBAUCVBVGVFVHVGVBVFVHJVGVBKZVCVDVEVIBVELMZVE
      ALMZKZDINZVJVECLMZKZDINZVCVDVBVMVPUDVGVBVLVODIVBVEIFZKVKVNVJVQVBVKVNJVQVB
      KVKACLMZVNVBVRVQACUEOVBVQAIFZCIFZKVKVRKVNJZVBVSVTACUFZACUGZUHVQVSVTWAVQVE
      TFVSATFVTCTFWAVEPAPCPVEACUIUJUKQULUMUNUOOVBVGVSVCVMRWBDBASQVBVGVTVDVPRWCD
      BCSQUPUQURUSUTVA $.
      $( [30-Jun-2013] $) $( [4-Oct-2005] $)

    $( A finite set of sequential integers is a subset of a set of upper
       integers. $)
    fzssuz $p |- ( M ... N ) C_ ( ZZ>= ` M ) $=
      ( vk cfz co cuz cfv cv elfzuz ssriv ) CABDEAFGCHABIJ $.
      $( [28-Oct-2005] $)
  $}

  ${
    $d k M $.  $d k N $.  $d k V $.
    $( Subset relationship for finite sets of sequential integers. $)
    fzssp1 $p |- ( N e. V -> ( M ... N ) C_ ( M ... ( N + 1 ) ) ) $=
      ( vk wcel cfz co c1 caddc cv wa cuz cfv elfzel2g uzid peano2uz 3syl fzss2
      wss cz syl simpr sseldd ex ssrdv ) BCEZDABFGZABHIGZFGZUFDJZUGEZUJUIEUFUKK
      ZUGUIUJULUHBLMZEZUGUISULBTEBUMEUNCUJABNBOBBPQBAUHRUAUFUKUBUCUDUE $.
      $( [21-Jul-2005] $)
  $}
  $( Append an element to a finite set of sequential integers. $)
  elfzp1 $p |- ( N e. ( ZZ>= ` M ) ->
    ( K e. ( M ... ( N + 1 ) ) <-> ( K e. ( M ... N ) \/ K = ( N + 1 ) ) ) ) $=
    ( wcel co cfz wceq wo wa cz cle wbr ad2antrr ad2antlr cr wb zre syl syl2anc
    mpbird cuz cfv c1 caddc wne wi w3a eluzel2 eluzelz elfzelz 3jca elfzle1 clt
    elfzle2 adantl anim1i peano2re 3syl ltlen zleltp1 jca32 elfz2 sylibrd eqcom
    ex adantr orbi1i neor orcom 3bitr3i sylib sseld peano2uz eluzfz2 syl5ibrcom
    fzssp1 eleq1 jaod impbid ) CBUAUBZDZABCUCUDEZFEZDZABCFEZDZAWBGZHZWAWDWHWAWD
    IZWBAUEZWFUFZWHWIWJBJDZCJDZAJDZUGZBAKLZACKLZIIZWFWIWJWRWIWJIZWOWPWQWSWLWMWN
    WAWLWDWJBCUHMWAWMWDWJBCUIZMZWDWNWAWJABWBUJZNZUKWDWPWAWJABWBULNWSWQAWBUMLZWS
    XDAWBKLZWJIZWIXEWJWDXEWAABWBUNUOUPWSAODZWBODZXDXFPWDXGWAWJWDWNXGXBAQRNWAXHW
    DWJWAWMCODXHWTCQCUQURMAWBUSSTWSWNWMWQXDPXCXAACUTSTVAVEWAWFWRPWDVTABCVBVFVCW
    BAGZWFHWGWFHWKWHXIWGWFWBAVDVGWFWBAVHWGWFVIVJVKVEWAWFWDWGWAWEWCABCVTVPVLWAWD
    WGWBWCDZWAWBVTDXJBCVMBWBVNRAWBWCVQVOVRVS $.
    $( [2-Sep-2013] $) $( [19-Sep-2005] $)

  ${
    $d M k $.  $d N k $.
    $( Join a successor to the end of a finite set of sequential integers.
       (Revised by Mario Carneiro, 7-Mar-2014.) $)
    fzsuc $p |- ( N e. ( ZZ>= ` M ) ->
                  ( M ... ( N + 1 ) ) = ( ( M ... N ) u. { ( N + 1 ) } ) ) $=
      ( vk cuz cfv wcel c1 caddc co cfz csn cun cv wceq elfzp1 elun elsn orbi2i
      wo bitri syl6bbr eqrdv ) BADEFZCABGHIZJIZABJIZUDKZLZUCCMZUEFUIUFFZUIUDNZS
      ZUIUHFZUIABOUMUJUIUGFZSULUIUFUGPUNUKUJCUDQRTUAUB $.
      $( [7-Mar-2014] $) $( [19-Jul-2008] $)
  $}

  $( Subset relationship for finite sets of sequential integers.  (Revised by
     Mario Carneiro, 30-Jun-2013.) $)
  fzp1ss $p |- ( ( M e. ZZ /\ N e. V ) ->
                ( ( M + 1 ) ... N ) C_ ( M ... N ) ) $=
    ( cz wcel c1 caddc co cuz cfv cfz wss uzid peano2uz syl fzss1 sylan ) ADEZA
    FGHZAIJZEZBCESBKHABKHLRATEUAAMAANOSABCPQ $.
    $( [30-Jun-2013] $) $( [26-Jul-2005] $)

  $( Membership in a set of sequential integers with an appended element. $)
  fzelp1 $p |- ( ( N e. A /\ K e. ( M ... N ) ) ->
                K e. ( M ... ( N + 1 ) ) ) $=
    ( wcel cfz co c1 caddc fzssp1 sselda ) DAECDFGCDHIGFGBCDAJK $.
    $( [2-Sep-2013] $) $( [7-Dec-2005] $)
  $( Add one to an element of a finite set of integers.  (Contributed by Jeff
     Madsen, 6-Jun-2010.) $)
  fzp1elp1 $p |- ( ( N e. A /\ K e. ( M ... N ) ) ->
                                  ( K + 1 ) e. ( M ... ( N + 1 ) ) ) $=
    ( wcel cfz co wa c1 caddc cz cvv wss elfzel1 adantl fzp1ss sylancl simpr wb
    ovex elfzel2g elfzelz 1z a1i fzaddel syl22anc mpbid sseldd ) DAEZBCDFGEZHZC
    IJGDIJGZFGZCULFGZBIJGZUKCKEZULLEUMUNMUJUPUIBCDNOZDIJTCULLPQUKUJUOUMEZUIUJRU
    KUPDKEBKEZIKEZUJURSUQABCDUAUJUSUIBCDUBOUTUKUCUDBICDUEUFUGUH $.
    $( [6-Jun-2010] $)

  ${
    fzelp1.1 $e |- N e. _V $.
    $( Membership in a set of sequential integers with an appended element. $)
    fzelp1i $p |- ( K e. ( M ... N ) -> K e. ( M ... ( N + 1 ) ) ) $=
      ( cvv wcel cfz co c1 caddc fzelp1 mpan ) CEFABCGHFABCIJHGHFDEABCKL $.
      $( [3-Dec-2005] $)
  $}


  ${
    $d M m $.
    $( A finite interval of integers with one element.  (Contributed by Jeff
       Madsen, 2-Sep-2009 ) $)
    fzsn $p |- ( M e. ZZ -> ( M ... M ) = { M } ) $=
      ( vm cz wcel cfz co csn cv elfz1eq elfz3 eleq1 syl5ibrcom impbid2 syl6bbr
      wceq elsn eqrdv ) ACDZBAAEFZAGZRBHZSDZUAAOZUATDRUBUCUAAIRUBUCASDAJUAASKLM
      BAPNQ $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)

    $( A finite interval of integers with two elements.  (Contributed by Jeff
       Madsen, 2-Sep-2009 ) $)
    fzpr $p |- ( M e. ZZ -> ( M ... ( M + 1 ) ) = { M , ( M + 1 ) } ) $=
      ( vm cz wcel c1 caddc co cfz cpr cv wceq cuz cfv uzid elfzp1 syl csn fzsn
      wo wb eleq2d elsn syl6bb orbi1d bitrd vex elpr syl6bbr eqrdv ) ACDZBAAEFG
      ZHGZAUKIZUJBJZULDZUNAKZUNUKKZSZUNUMDUJUOUNAAHGZDZUQSZURUJAALMDUOVATANUNAA
      OPUJUTUPUQUJUTUNAQZDUPUJUSVBUNARUABAUBUCUDUEUNAUKBUFUGUHUI $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)

    $( A finite interval of integers with three elements. $)
    fztp $p |- ( M e. ZZ -> ( M ... ( M + 2 ) )
                 = { M , ( M + 1 ) , ( M + 2 ) } ) $=
      ( cz wcel c1 caddc co cfz csn cun c2 ctp cuz cfv wceq uzid peano2uz fzsuc
      cc ax-1cn syl6eqr 3syl zcn addass mp3an23 syl df-2 oveq2i oveq2d cpr fzpr
      sneqd uneq12d df-tp 3eqtr3d ) ABCZAADEFZDEFZGFZAUPGFZUQHZIZAAJEFZGFAUPVBK
      ZUOAALMZCUPVDCURVANAOAAPAUPQUAUOUQVBAGUOUQADDEFZEFZVBUOARCZUQVFNZAUBVGDRC
      ZVIVHSSADDUCUDUEJVEAEUFUGTZUHUOVAAUPUIZVBHZIVCUOUSVKUTVLAUJUOUQVBVJUKULAU
      PVBUMTUN $.
      $( [7-Mar-2014] $) $( [13-Sep-2011] $)
  $}

  ${
    $d M k $.  $d N k $.
    $( Join a successor to the end of a finite set of sequential integers.
       (Contributed by Mario Carneiro, 7-Mar-2014.) $)
    fzsuc2 $p |- ( ( M e. ZZ /\ N e. ( ZZ>= ` ( M - 1 ) ) ) ->
                   ( M ... ( N + 1 ) ) = ( ( M ... N ) u. { ( N + 1 ) } ) ) $=
      ( c1 cmin co cuz cfv wcel cz wceq caddc cfz csn cun c0 oveq2d syl uneq12d
      cc sneqd wo uzp1 uncom un0 eqtri syl6eqr zcn ax-1cn npcan sylancl clt wbr
      fzsn cr zre ltm1 wb peano2zm mpdan mpbid 3eqtr4d oveq1 eqeq12d syl5ibrcom
      fzn oveq2 imp wa fveq2d eleq2d biimpa fzsuc jaodan sylan2 ) BACDEZFGHAIHZ
      BVOJZBVOCKEZFGZHZUAABCKEZLEZABLEZWAMZNZJZVOBUBVPVQWFVTVPVQWFVPWFVQAVRLEZA
      VOLEZVRMZNZJVPAALEZOAMZNZWGWJVPWKWLWMAUMWMWLONWLOWLUCWLUDUEUFVPVRAALVPASH
      CSHVRAJAUGUHACUIUJZPVPWHOWIWLVPVOAUKULZWHOJZVPAUNHWOAUOAUPQVPVOIHWOWPUQAU
      RAVOVEUSUTVPVRAWNTRVAVQWBWGWEWJVQWAVRALBVOCKVBZPVQWCWHWDWIBVOALVFVQWAVRWQ
      TRVCVDVGVPVTVHBAFGZHZWFVPVTWSVPVSWRBVPVRAFWNVIVJVKABVLQVMVN $.
      $( [7-Mar-2014] $)
  $}

  $( ` ( M ... ( N + 1 ) ) ` is the disjoint union of ` ( M ... N ) ` with
     ` { ( N + 1 ) } ` .  (Contributed by Mario Carneiro, 7-Mar-2014.) $)
  fzp1disj $p |- ( N e. V -> ( ( M ... N ) i^i { ( N + 1 ) } ) = (/) ) $=
    ( wcel c1 caddc co cfz wn csn cin c0 wceq cle wbr elfzle2 adantl wa cz cr
    elfzel2g zre ltp1 wb peano2re ltnle mpdan mpbid 3syl pm2.65da disjsn sylibr
    clt ) BCDZBEFGZABHGZDZIUPUOJKLMUNUQUOBNOZUQURUNUOABPQUNUQRBSDBTDZURIZCUOABU
    ABUBUSBUOUMOZUTBUCUSUOTDVAUTUDBUEBUOUFUGUHUIUJUPUOUKUL $.
    $( [7-Mar-2014] $)

  ${
    $d x A $.  $d x B $.  $d x F $.
    $( Two ways of defining the first two values of a sequence on ` NN ` . $)
    fzprval $p |- ( A. x e. ( 1 ... 2 ) ( F ` x ) = if ( x = 1 , A , B )
                  <-> ( ( F ` 1 ) = A /\ ( F ` 2 ) = B ) ) $=
      ( cfv c1 wceq c2 cfz co wral cpr wsbc wa df-2 cr 1re elexi 2re fveq2 wcel
      cv cif caddc cz 1z fzpr ax-mp oveq2i preq2i 3eqtr4i raleqi iftrue eqeq12d
      ralpr sbcie wn wne 1lt2 ltneii pm13.181 mpan2 df-ne sylib iffalse anbi12i
      syl 3bitri ) AUBZDEZVIFGZBCUCZGZAFHIJZKVMAFHLZKVMAFMZVMAHMZNFDEZBGZHDEZCG
      ZNVMAVNVOFFFUDJZIJZFWBLZVNVOFUEUAWCWDGUFFUGUHHWBFIOUIHWBFOUJUKULVMAFHFPQR
      ZHPSRZUOVPVSVQWAVMVSAFWEVKVJVRVLBVIFDTVKBCUMUNUPVMWAAHWFVIHGZVJVTVLCVIHDT
      WGVKUQZVLCGWGVIFURZWHWGHFURWIFHQSUSUTVIHFVAVBVIFVCVDVKBCVEVGUNUPVFVH $.
      $( [5-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x F $.
    $( Two ways of defining the first three values of a sequence on ` NN ` . $)
    fztpval $p |- ( A. x e. ( 1 ... 3 ) ( F ` x )
                      = if ( x = 1 , A , if ( x = 2 , B , C ) )
                  <-> ( ( F ` 1 ) = A /\ ( F ` 2 ) = B /\ ( F ` 3 ) = C ) ) $=
      ( cfv c1 wceq c2 c3 cfz co ctp wsbc caddc ax-mp elexi 3re fveq2 wne cv cz
      cif wral w3a wcel 1z fztp df-3 2cn ax-1cn addcomi eqtri oveq2i tpeq3 df-2
      tpeq2 3eqtr4i raleqi cc cr raltp iftrue eqeq12d sbcie 1re 2re 1lt2 ltneii
      pm13.181 mpan2 df-ne sylib iffalse syl eqtrd 1lt3 2lt3 3anbi123i 3bitri
      wn ) AUAZEFZWBGHZBWBIHZCDUCZUCZHZAGJKLZUDWHAGIJMZUDWHAGNZWHAINZWHAJNZUEGE
      FZBHZIEFZCHZJEFZDHZUEWHAWIWJGGIOLZKLZGGGOLZWTMZWIWJGUBUFXAXCHUGGUHPJWTGKJ
      IGOLWTUIIGUJUKULUMZUNWJGIWTMZXCJWTHWJXEHXDJWTGIUOPIXBHXEXCHUPIXBGWTUQPUMU
      RUSWHAGIJGUTUKQZIUTUJQZJVARQZVBWKWOWLWQWMWSWHWOAGXFWDWCWNWGBWBGESWDBWFVCV
      DVEWHWQAIXGWEWCWPWGCWBIESWEWGWFCWEWDWAZWGWFHZWEWBGTZXIWEIGTXKGIVFVGVHVIWB
      IGVJVKWBGVLZVMWDBWFVNZVOWECDVCVPVDVEWHWSAJXHWBJHZWCWRWGDWBJESXNWGWFDXNXIX
      JXNXKXIXNJGTXKGJVFRVQVIWBJGVJVKXLVMXMVOXNWEWAZWFDHXNWBITZXOXNJITXPIJVGRVR
      VIWBJIVJVKWBIVLVMWECDVNVOVPVDVEVSVT $.
      $( [13-Sep-2011] $)
  $}

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( K e. ( ( J - N ) ... ( J - M ) ) <-> ( J - K ) e. ( M ... N ) ) ) $=
    ( cz wcel wa cmin co cle wbr cfz wb cr zre syl3an 3expb zsubcl ancoms elfz
    ancom suble 3comr adantll lesub adantlr anbi12d syl5rbbr ad2ant2lr ad2ant2r
    simprr syl3anc adantl simpll simplr 3bitr4d ) CEFZDEFZGZAEFZBEFZGZGZADHIZBJ
    KZBACHIZJKZGZCABHIZJKZVIDJKZGZBVDVFLIFZVICDLIFZVLVKVJGVCVHVKVJUAVCVKVEVJVGU
    RVBVKVEMZUQURUTVAVOUTVAURVOUTANFZVABNFZURDNFVOAOZBOZDOABDUBPUCQUDUQVBVJVGMZ
    URUQUTVAVTUQCNFUTVPVAVQVTCOVRVSCABUEPQUFUGUHVCVAVDEFZVFEFZVMVHMUSUTVAUKURUT
    WAUQVAUTURWAADRSUIUQUTWBURVAUTUQWBACRSUJBVDVFTULVCVIEFZUQURVNVLMVBWCUSABRUM
    UQURVBUNUQURVBUOVICDTULUP $.
    $( [25-Nov-2005] $)

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev2 $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( K e. ( M ... N ) <-> ( J - K ) e. ( ( J - N ) ... ( J - M ) ) ) ) $=
    ( cz wcel wa cmin co cfz wb simpl zsubcl jca fzrev sylan2 wceq cc zcn nncan
    syl2an adantl eleq1d bitr2d ) CEFDEFGZAEFZBEFZGZGZABHIZADHIACHIJIFZAUJHIZCD
    JIZFZBUMFUHUEUFUJEFZGUKUNKUHUFUOUFUGLABMNAUJCDOPUIULBUMUHULBQZUEUFARFBRFUPU
    GASBSABTUAUBUCUD $.
    $( [25-Nov-2005] $)

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev2i $p |- ( ( N e. A /\ J e. ZZ /\ K e. ( M ... N ) )
                 -> ( J - K ) e. ( ( J - N ) ... ( J - M ) ) ) $=
    ( wcel cz cfz co w3a cmin simp3 elfzel1 adantl elfzel2g jca 3adant2 elfzelz
    wa wb anim2i 3adant1 fzrev2 syl2anc mpbid ) EAFZBGFZCDEHIFZJZUHBCKIBEKIBDKI
    HIFZUFUGUHLUIDGFZEGFZSZUGCGFZSZUHUJTUFUHUMUGUFUHSUKULUHUKUFCDEMNACDEOPQUGUH
    UOUFUHUNUGCDERUAUBBCDEUCUDUE $.
    $( [25-Nov-2005] $)

  $( The "complement" of a member of a finite set of sequential integers. $)
  fzrev3 $p |- ( ( N e. A /\ K e. ZZ ) -> ( K e. ( M ... N )
                <-> ( ( M + N ) - K ) e. ( M ... N ) ) ) $=
    ( wcel cz wa cfz co cmin simplr elfzel1 adantl elfzel2g adantlr 3jca wb zcn
    cc 3adant1 caddc w3a pncan pncan2 oveq12d syl2an eleq2d 3simpc zaddcl simp1
    wceq fzrev syl12anc bitr3d pm5.21nd ) DAEZBFEZGZBCDHIZEZCDUAIZBJIZUSEZUQCFE
    ZDFEZUBZURUTGUQVDVEUPUQUTKUTVDURBCDLMUPUTVEUQABCDNOPURVCGUQVDVEUPUQVCKVCVDU
    RVBCDLMUPVCVEUQAVBCDNOPVFBVADJIZVACJIZHIZEZUTVCVDVEVJUTQUQVDVEGZVIUSBVDCSEZ
    DSEZVIUSUKVECRDRVLVMGVGCVHDHCDUCCDUDUEUFUGTVFVKVAFEZUQVJVCQUQVDVEUHVDVEVNUQ
    CDUITUQVDVEUJVABCDULUMUNUO $.
    $( [20-Nov-2005] $)

  $( The "complement" of a member of a finite set of sequential integers. $)
  fzrev3i $p |- ( ( N e. A /\ K e. ( M ... N ) )
                -> ( ( M + N ) - K ) e. ( M ... N ) ) $=
    ( wcel cfz co wa caddc cmin simpr cz wb elfzelz fzrev3 sylan2 mpbid ) DAEZB
    CDFGZEZHTCDIGBJGSEZRTKTRBLETUAMBCDNABCDOPQ $.
    $( [20-Nov-2005] $)

  $( Finite set of sequential integers starting at 0. $)
  fznn0 $p |- ( N e. NN0
              -> ( K e. ( 0 ... N ) <-> ( K e. NN0 /\ K <_ N ) ) ) $=
    ( cn0 wcel cc0 cfz co cz cle wbr w3a wa wb nn0z elfz1 sylancr df-3an elnn0z
    0z anbi1i bitr4i syl6bb ) BCDZAEBFGDZAHDZEAIJZABIJZKZACDZUGLZUCEHDBHDUDUHMS
    BNAEBOPUHUEUFLZUGLUJUEUFUGQUIUKUGARTUAUB $.
    $( [1-Aug-2005] $)

  $( Finite set of sequential integers starting at 1. $)
  fznn $p |- ( N e. NN
              -> ( K e. ( 1 ... N ) <-> ( K e. NN /\ K <_ N ) ) ) $=
    ( cn wcel c1 cfz co cz cle wbr w3a wa wb 1z nnz elfz1 sylancr df-3an elnnz1
    anbi1i bitr4i syl6bb ) BCDZAEBFGDZAHDZEAIJZABIJZKZACDZUGLZUCEHDBHDUDUHMNBOA
    EBPQUHUEUFLZUGLUJUEUFUGRUIUKUGASTUAUB $.
    $( [31-Aug-2011] $)

  $( Membership in a finite set of sequential integers.  (Contributed by Paul
     Chapman, 21-Mar-2011.) $)
  elfzm11 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... ( N - 1 ) ) <->
                                         ( K e. ZZ /\ M <_ K /\ K < N ) ) ) $=
    ( cz wcel wa c1 cmin co cfz cle wbr w3a clt wb peano2zm elfz1 sylan2 3anass
    zltlem1 anbi2d expcom pm5.32d 3bitr4g adantl bitr4d ) BDEZCDEZFABCGHIZJIEZA
    DEZBAKLZAUIKLZMZUKULACNLZMZUHUGUIDEUJUNOCPABUIQRUHUPUNOUGUHUKULUOFZFUKULUMF
    ZFUPUNUHUKUQURUKUHUQUROUKUHFUOUMULACTUAUBUCUKULUOSUKULUMSUDUEUF $.
    $( [21-Mar-2011] $)

  $( Lemma for theorems about the central binomial coefficient.  (Contributed
     by Mario Carneiro, 8-Mar-2014.) $)
  fzctr $p |- ( N e. NN0 -> N e. ( 0 ... ( 2 x. N ) ) ) $=
    ( cn0 wcel cc0 c2 cmul co cfz cle wbr cz wa wb nn0z a1i zmulcl sylancr elfz
    0z 2z syl3anc nn0ge0 caddc cr nn0re nn0addge1 mpancom wceq nn0cn 2times syl
    cc breqtrrd mpbir2and ) ABCZADEAFGZHGCZDAIJZAUPIJZUOAKCZDKCZUPKCZUQURUSLMAN
    ZVAUOSOUOEKCUTVBTVCEAPQADUPRUAAUBUOAAAUCGZUPIAUDCUOAVDIJAUEAAUFUGUOAULCUPVD
    UHAUIAUJUKUMUN $.
    $( [2-Aug-2014] $) $( [8-Mar-2014] $)

  ${
    $d k M $.  $d k N $.
    $( Express an upper integer set as the disjoint (see ~ uzdisj ) union of
       the first ` N ` values and the rest.  (Contributed by Mario Carneiro,
       24-Apr-2014.) $)
    uzsplit $p |- ( N e. ( ZZ>= ` M ) -> ( ZZ>= ` M ) =
                  ( ( M ... ( N - 1 ) ) u. ( ZZ>= ` N ) ) ) $=
      ( vk cuz cfv wcel c1 cmin co cfz wo wa cle wbr cr eluzelre syl2an eluzelz
      cz wb cun clt lelttric eluz eluzel2 w3a elfzm11 df-3an syl6bb syl2anr jca
      cv eluzle adantl biantrurd bitr4d orbi12d mpbird orcomd elfzuz a1i expcom
      ex wi uztrn jaod impbid elun syl6bbr eqrdv ) BADEZFZCVKABGHIZJIZBDEZUAZVL
      CULZVKFZVQVNFZVQVOFZKZVQVPFVLVRWAVLVRWAVLVRLZVTVSWBVTVSKBVQMNZVQBUBNZKZVL
      BOFVQOFWEVRABPAVQPBVQUCQWBVTWCVSWDVLBSFZVQSFZVTWCTVRABRZAVQRZBVQUDQWBVSWG
      AVQMNZLZWDLZWDVRASFZWFVSWLTVLAVQUEWHWMWFLVSWGWJWDUFWLVQABUGWGWJWDUHUIUJWB
      WKWDVRWKVLVRWGWJWIAVQUMUKUNUOUPUQURUSVCVLVSVRVTVSVRVDVLVQAVMUTVAVTVLVRBVQ
      AVEVBVFVGVQVNVOVHVIVJ $.
      $( [24-Apr-2014] $)

    $( The first ` N ` elements of an upper integer set are distinct from any
       later members.  (Contributed by Mario Carneiro, 24-Apr-2014.) $)
    uzdisj $p |- ( ( M ... ( N - 1 ) ) i^i ( ZZ>= ` N ) ) = (/) $=
      ( vk c1 cmin co cfz cuz cfv c0 wcel wbr cle syl cz wb syl2anc mpbid zre
      cr cin wss wceq cv elin simprbi eluzle eluzel2 eluzelz zlem1lt wn simplbi
      clt elfzle2 peano2zm lenlt pm2.65i pm2.21i ssriv ss0 ax-mp ) ABDEFZGFZBHI
      ZUAZJUBVEJUCCVEJCUDZVEKZVFJKVGVBVFUMLZVGBVFMLZVHVGVFVDKZVIVGVFVCKZVJVFVCV
      DUEZUFZBVFUGNVGBOKZVFOKZVIVHPVGVJVNVMBVFUHNZVGVJVOVMBVFUINZBVFUJQRVGVFVBM
      LZVHUKZVGVKVRVGVKVJVLULVFAVBUNNVGVFTKZVBTKZVRVSPVGVOVTVQVFSNVGVBOKZWAVGVN
      WBVPBUONVBSNVFVBUPQRUQURUSVEUTVA $.
      $( [24-Apr-2014] $)
  $}

  ${
    fseq1p1m1.1 $e |- H = { <. ( N + 1 ) , B >. } $.
    $( Add/remove an item to/from the end of a finite sequence.  (Contributed
       by Paul Chapman, 17-Nov-2012.)  (Revised by Mario Carneiro,
       7-Mar-2014.) $)
    fseq1p1m1 $p |- ( N e. NN0 ->
        ( ( F : ( 1 ... N ) --> A /\ B e. A /\ G = ( F u. H ) ) <->
          ( G : ( 1 ... ( N + 1 ) ) --> A /\ ( G ` ( N + 1 ) ) = B /\
            F = ( G |` ( 1 ... N ) ) ) ) ) $=
      ( cn0 wcel c1 co wf cun wceq cfv cres c0 syl2anc syl 3syl cfz w3a csn cin
      caddc wa simpr1 wss cn nn0p1nn adantr simpr2 cop fsng mpbiri fss fzp1disj
      snssd fun syl21anc cz cmin cuz 1z simpl nn0uz ax-1cn subidi fveq2i eqtr4i
      cc0 syl6eleq fzsuc2 sylancr eqcomd unidm feq23d mpbid simpr3 feq1d mpbird
      a1i ovex snid fvres ax-mp reseq1 wfn wb ffn fnresdisj uneq1d resundir un0
      uncom eqtr2i 3eqtr4g 3eqtrd fveq1d fveq1i fvsng syl5eq eqtrd incom uneq2d
      fnresdm syl5eqr eqcomi 3eqtrrd 3jca fzssp1 fssres eluzfz2 ffvelrn fnressn
      eqeltrrd opeq2 sneqd syl6reqr uneq12d reseq2d resundi syl6req impbida
      nnuz ) FHIZJFUAKZACLZBAIZDCEMZNZUBZJFJUEKZUAKZADLZYMDOZBNZCDYGPZNZUBZYFYL
      UFZYOYQYSUUAYOYNAYJLZUUAYGYMUCZMZAAMZYJLZUUBUUAYHUUCAELZYGUUCUDZQNZUUFYFY
      HYIYKUGZUUAUUCBUCZELZUUKAUHUUGUUAYMUIIZYIUULYFUUMYLFUJZUKZYFYHYIYKULZUUMY
      IUFZUULEYMBUMZUCZNGYMBUIAEUNUORZUUABAUUPURUUCUUKAEUPRZYFUUIYLJFHUQUKZYGUU
      CAACEUSUTUUAUUDUUEYNAYJUUAYNUUDUUAJVAIZFJJVBKZVCOZIZYNUUDNZVDUUAFHUVEYFYL
      VEHVKVCOUVEVFUVDVKVCJVGVHVIVJZVLJFVMZVNVOUUEANUUAAVPWBVQVRUUAYNADYJYFYHYI
      YKVSZVTWAUUAYPYMDUUCPZOZBYMUUCIUVLYPNYMFJUEWCWDYMUUCDWEWFUUAUVLYMEOZBUUAY
      MUVKEUUAUVKYJUUCPZEUUCPZEUUAYKUVKUVNNUVJDYJUUCWGSUUACUUCPZUVOMQUVOMZUVNUV
      OUUAUVPQUVOUUAUUIUVPQNZUVBUUAYHCYGWHZUUIUVRWIUUJYGACWJZYGUUCCWKTVRWLCEUUC
      WMUVQUVOQMUVOQUVOWOUVOWNWPWQUUAUUGEUUCWHZUVOENUVAUUCAEWJUUCEXFTWRWSUUAUUM
      YIUVMBNUUOUUPUUQUVMYMUUSOBYMEUUSGWTYMBUIAXAXBRXCXGUUAYRYJYGPZCYGPZCUUAYKY
      RUWBNUVJDYJYGWGSUUAUWCEYGPZMUWCQMZUWBUWCUUAUWDQUWCUUAUUCYGUDZQNZUWDQNZUUA
      UWFUUHQUUCYGXDUVBXBUUAUULUWAUWGUWHWIUUTUUCUUKEWJUUCYGEWKTVRXECEYGWMUWEUWC
      UWCWNXHWQUUAYHUVSUWCCNUUJUVTYGCXFTXIXJYFYTUFZYHYIYKUWIYHYGAYRLZUWIYOYGYNU
      HZUWJYFYOYQYSUGZYFUWKYTJFHXKUKYNAYGDXLRUWIYGACYRYFYOYQYSVSZVTWAUWIYPBAYFY
      OYQYSULZUWIYOYMYNIZYPAIUWLUWIYMJVCOZIUWOUWIYMUIUWPYFUUMYTUUNUKYEVLJYMXMSZ
      YNAYMDXNRXPUWIYJYRUVKMZDYNPZDUWICYREUVKUWMUWIUVKUUSEUWIUVKYMYPUMZUCZUUSUW
      IDYNWHZUWOUVKUXANUWIYOUXBUWLYNADWJZSUWQYNYMDXORUWIYQUXAUUSNUWNYQUWTUURYPB
      YMXQXRSXCGXSXTUWIUWSDUUDPUWRUWIYNUUDDUWIUVCUVFUVGVDUWIFHUVEYFYTVEUVHVLUVI
      VNYADYGUUCYBYCUWIYOUXBUWSDNUWLUXCYNDXFTXIXJYD $.
      $( [17-Nov-2012] $)
  $}

  ${
    fseq1m1p1.1 $e |- H = { <. N , B >. } $.
    $( Add/remove an item to/from the end of a finite sequence.  (Contributed
       by Paul Chapman, 17-Nov-2012.) $)
    fseq1m1p1 $p |- ( N e. NN ->
        ( ( F : ( 1 ... ( N - 1 ) ) --> A /\ B e. A /\ G = ( F u. H ) ) <->
          ( G : ( 1 ... N ) --> A /\ ( G ` N ) = B /\
            F = ( G |` ( 1 ... ( N - 1 ) ) ) ) ) ) $=
      ( cn wcel c1 co cfz wf cop csn cun wceq w3a cfv cc cmin caddc cres cn0 wb
      nnm1nn0 eqid fseq1p1m1 syl nncn ax-1cn npcan sylancl opeq1d sneqd syl6eqr
      uneq2d eqeq2d 3anbi3d oveq2d feq2d fveq2d eqeq1d 3anbi12d 3bitr3d ) FHIZJ
      FJUAKZLKZACMZBAIZDCVGJUBKZBNZOZPZQZRZJVKLKZADMZVKDSZBQZCDVHUCQZRZVIVJDCEP
      ZQZRJFLKZADMZFDSZBQZWARVFVGUDIVPWBUEFUFABCDVMVGVMUGUHUIVFVOWDVIVJVFVNWCDV
      FVMECVFVMFBNZOEVFVLWIVFVKFBVFFTIJTIVKFQFUJUKFJULUMZUNUOGUPUQURUSVFVRWFVTW
      HWAVFVQWEADVFVKFJLWJUTVAVFVSWGBVFVKFDWJVBVCVDVE $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d k N $.
    $( Quantification over a one-member finite set of sequential integers in
       terms of substitution. $)
    fz1sbc $p |- ( N e. ZZ -> ( A. k e. ( N ... N ) ph <-> [ N / k ] ph ) ) $=
      ( cz wcel wsbc cv wceq wi wal cfz co wral sbc6g df-ral elfz1eq syl5ibrcom
      elfz3 eleq1 impbid2 imbi1d albidv syl5rbb bitr2d ) CDEZABCFBGZCHZAIZBJZAB
      CCKLZMZABCDNUKUFUJEZAIZBJUEUIABUJOUEUMUHBUEULUGAUEULUGUFCPUEULUGCUJECRUFC
      UJSQTUAUBUCUD $.
      $( [28-Nov-2005] $)
  $}

  $( An integer is a member of a 1-based finite set of sequential integers iff
     its predecessor is a member of the corresponding 0-based set.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  elfzm1b $p |- ( ( K e. ZZ /\ N e. ZZ ) ->
                ( K e. ( 1 ... N ) <-> ( K - 1 ) e. ( 0 ... ( N - 1 ) ) ) ) $=
    ( cz wcel c1 cfz co cc0 wb wa 1z fzsubel mpanl1 mpanr2 ax-1cn subidi oveq1i
    cmin eleq2i syl6bb ancoms ) BCDZACDZAEBFGDZAERGZHBERGZFGZDZIUBUCJUDUEEERGZU
    FFGZDZUHUBUCECDZUDUKIZKULUBUCULJUMKAEEBLMNUJUGUEUIHUFFEOPQSTUA $.
    $( [22-Jun-2011] $)

  $( Options for membership in a finite interval of integers.  (Contributed by
     Jeff Madsen, 18-Jun-2010.) $)
  elfzp12 $p |- ( N e. ( ZZ>= ` M ) -> ( K e. ( M ... N ) <->
                                ( K = M \/ K e. ( ( M + 1 ) ... N ) ) ) ) $=
    ( cuz cfv wcel cfz co wceq c1 caddc wo cvv wa elex anim2i eluzel2 syl cun
    cz eleq1 syl5ibrcom imdistani jaodan csn eluzfz1 fzsplit mpdan uneq1d eqtrd
    fzsn eleq2d elun syl6bb elsncg orbi1d sylan9bb pm5.21nd ) CBDEZFZABCGHZFZAB
    IZABJKHCGHZFZLZUTAMFZNZVBVGUTAVAOPUTVCVHVEUTVCVGUTVGVCBMFZUTBTFZVIBCQZBTORA
    BMUAUBUCVEVGUTAVDOPUDUTVBABUEZFZVELZVGVFUTVBAVLVDSZFVNUTVAVOAUTVABBGHZVDSZV
    OUTBVAFVAVQIBCUFUSBBCUGUHUTVPVLVDUTVJVPVLIVKBUKRUIUJULAVLVDUMUNVGVMVCVEABMU
    OUPUQUR $.
    $( [18-Jun-2010] $)

  $( Choices for an element of a finite interval of integers.  (Contributed by
     Jeff Madsen, 2-Sep-2009.) $)
  fzm1 $p |- ( N e. ( ZZ>= ` M ) -> ( K e. ( M ... N ) <->
                              ( K e. ( M ... ( N - 1 ) ) \/ K = N ) ) ) $=
    ( wcel cfz co c1 cmin wceq wa wi cz cle wbr w3a wb elfz1 syl2anc a1i ex cuz
    cfv wo wn eluzel2 eluzelz simp1 simp2 clt wne cr zre eluzelre ltlen syl2anr
    necom df-ne bitri anbi2i syl6bb biimpar an4s zltlem1 sylan2 biimpd ad2ant2r
    ancom ancoms mpd 3adantr2 3jcad 1z zsubcl sylancl biimprd syl6d sylbid orrd
    com23 imp orcomd caddc cvv wss ovex fzssp1 ax-mp cc zcn ax-1cn npcan oveq2d
    syl syl5sseq sseld eluzfz2 eleq1 syl5ibrcom jaod impbid ) CBUAUBDZABCEFZDZA
    BCGHFZEFZDZACIZUCZXAXCXHXAXCJZXGXFXIXGXFXAXCXGUDZXFKZXAXCALDZBAMNZACMNZOZXK
    XABLDZCLDZXCXOPBCUEZBCUFZABCQRXAXJXOXFXAXJXOXLXMAXDMNZOZXFXAXJXOYAKXAXJJZXO
    XLXMXTXOXLKYBXLXMXNUGSXOXMKYBXLXMXNUHSYBXOXTYBXLXNXTXMYBXLXNJJACUINZXTXAXLX
    JXNYCXAXLJZYCXJXNJZYDYCXNCAUJZJZYEXLAUKDCUKDYCYGPXAAULBCUMACUNUOYGXNXJJYEYF
    XJXNYFACUJXJCAUPACUQURUSXNXJVGURUTVAVBXAXLYCXTKZXJXNXLXAYHXLXAJYCXTXAXLXQYC
    XTPXSACVCVDVEVHVFVIVJTVKTXAXFYAXAXPXDLDZXFYAPXRXAXQGLDYIXSVLCGVMVNABXDQRVOV
    PVSVQVTVRWATXAXFXCXGXAXEXBAXABXDGWBFZEFZXEXBXDWCDXEYKWDCGHWEBXDWCWFWGXAYJCB
    EXACWHDZGWHDYJCIXAXQYLXSCWIWMWJCGWKVNWLWNWOXAXCXGCXBDBCWPACXBWQWRWSWT $.
    $( [2-Sep-2009] $)

  $( No finite set of sequential integers equals a set of upper integers. $)
  fzneuz $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. ZZ )
               -> -. ( M ... N ) = ( ZZ>= ` K ) ) $=
    ( cuz cfv wcel cz wa cfz co wceq wn c1 caddc peano2uz adantl wbr cr nelneq2
    ad2antrr cle eluzelz zre clt ltp1 wb peano2re ltnle mpdan 3syl elfzle2 nsyl
    mpbid syl2anc eqcom sylnib eluzfz2 sylancom pm2.61dan ) CBDEFZAGFZHZCADEZFZ
    BCIJZVCKZLZVBVDHZVCVEKZVFVHCMNJZVCFZVJVEFZLZVILVDVKVBACOPUTVMVAVDUTVJCUAQZV
    LUTCGFCRFZVNLZBCUBCUCVOCVJUDQZVPCUEVOVJRFVQVPUFCUGCVJUHUIUMUJVJBCUKULTVJVCV
    ESUNVCVEUOUPVBVDLZCVEFZVGUTVSVAVRBCUQTCVEVCSURUS $.
    $( [11-Dec-2005] $)

  ${
    $d M k n $.  $d N k n $.
    $( A convenience lemma for the sequential integer function showing
       existence of the second argument with no constraints to start with.
       (Contributed by Mario Carneiro, 13-Aug-2013.) $)
    fz2ex $p |- E. n e. ZZ ( M ... N ) = ( M ... n ) $=
      ( vk cz wcel cfz co cv wceq cvv wex wn oveq2 eqeq2d rcla4ev c0 sylbi cc0
      wa wrex wi ovprc2 sylan2 expcom n0 necon1bbii c1 cmin peano2zm a1i clt cr
      wbr zre ltm1 syl fzn mpdan mpbid biimprcd jcad syl6 elfzel2g eqid sylancl
      wb ex exlimdv imp a1d ecase 0z wne elfzel1 exlimiv eqtr4d sylancr pm2.61i
      necon1bi ) BEFZBCGHZBAIZGHZJZAEUAZCKFZDIZWBFZDLZWAWFUBZWAWGMZWFWLWAWBBBGH
      ZJZWFBCGUCWEWNABEWCBJWDWMWBWCBBGNOPUDUEWJMWBQJZWKWJWBQDWBUFZUGWOWABUHUIHZ
      EFZWBBWQGHZJZTWFWOWAWRWTWAWRUBWOBUJZUKWAWTWOWAWSQWBWAWQBULUNZWSQJZWABUMFX
      BBUOBUPUQWAWRXBXCVGXABWQURUSUTOVAVBWEWTAWQEWCWQJWDWSWBWCWQBGNOPVCRWGWJTWF
      WAWGWJWFWGWIWFDWGWIWFWGWITCEFWBWBJZWFKWHBCVDWBVEWEXDACEWCCJWDWBWBWCCBGNOP
      VFVHVIVJVKVLWAMZSEFWBBSGHZJZWFVMXEWBQXFWAWBQWBQVNWJWAWPWIWADWHBCVOVPRVTWA
      XFQXFQVNWHXFFZDLWADXFUFXHWADWHBSVOVPRVTVQWEXGASEWCSJWDXFWBWCSBGNOPVRVS $.
      $( [13-Aug-2013] $)
  $}

  $( Disjointness of the upper integers and a finite sequence.  (Revised by
     Mario Carneiro, 30-Jun-2013.) $)
  fznuz $p |- ( ( N e. V /\ K e. ( M ... N ) ) ->
      -. K e. ( ZZ>= ` ( N + 1 ) ) ) $=
    ( wcel cfz co wa c1 caddc cuz cfv cle wbr elfzle2 adantl clt cz cr zre mt2d
    wn wi elfzel2g eluzp1l ex syl wb elfzelz ltnle syl2an syl2anc sylibd ) CDEZ
    ABCFGEZHZACIJGKLEZACMNZUOURUNABCOPUPUQCAQNZURUBZUPCREZUQUSUCDABCUDZVAUQUSCA
    UEUFUGUPVAAREZUSUTUHZVBUOVCUNABCUIPVACSEASEVDVCCTATCAUJUKULUMUA $.
    $( [24-Aug-2013] $)

  $( Disjointness of the upper integers and a finite sequence.  (Contributed by
     Mario Carneiro, 24-Aug-2013.) $)
  uznfz $p |- ( K e. ( ZZ>= ` N ) -> -. K e. ( M ... ( N - 1 ) ) ) $=
    ( cuz cfv wcel c1 cmin co cfz cle wbr eluzle clt wn cz eluzel2 wi cr zre wa
    elfzel1 w3a elfzm11 simp3 syl6bi impancom mpancom syl5com wb eluzelz syl2an
    ltnle syl2anc sylibd mt2d ) ACDEFZABCGHIZJIFZCAKLZCAMUQUSACNLZUTOZUQCPFZUSV
    ACAQZBPFZUSVCVARABURUBVEVCUSVAVEVCUAUSAPFZBAKLZVAUCVAABCUDVFVGVAUEUFUGUHUIU
    QVFVCVAVBUJZCAUKVDVFASFCSFVHVCATCTACUMULUNUOUP $.
    $( [24-Aug-2013] $)

  ${
    $d j k x K $.  $d j k x M $.  $d j k x N $.  $d k x ph $.
    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( M ... N
       ) ph <-> A. k e. ( ( K - N ) ... ( K - M ) ) [ ( K - k ) / j ] ph ) ) $=
      ( cz wcel cfz co wral cv cmin wsbc wi wa wb elfzelz syl wceq simpr sylan2
      w3a fzrev anassrs mpbid ra4sbc ex 3impa com23 ralrimdv ax-17 ovex hbsbc1v
      hbral fzrev2i 3expa oveq2 dfsbcq rcla4v zcn syl2an eqcomd sbceq1a adantll
      cc nncan sylibrd ralrimd 3adant1 impbid ) EGHZFGHZDGHZUCZABEFIJZKZABDCLZM
      JZNZCDFMJZDEMJZIJZKZVOVQVTCWCVOVRWCHZVQVTVLVMVNWEVQVTOZOVLVMPZVNPZWEWFWHW
      EPZVSVPHZWFWIWEWJWHWEUAWEWHVRGHZWEWJQZVRWAWBRWGVNWKWLDVREFUDUEUBUFABVSVPU
      GSUHUIUJUKVMVNWDVQOVLVMVNPZWDABVPWMBULVTBCWCWEBULABVSDVRMUMUNUOWMBLZVPHZW
      DAWMWOWDAOWMWOPZWDABDDWNMJZMJZNZAWPWQWCHZWDWSOVMVNWOWTGDWNEFUPUQVTWSCWQWC
      VRWQTVSWRTVTWSQVRWQDMURABVSWRUSSUTSVNWOAWSQZVMVNWOPZWNWRTXAXBWRWNVNDVFHWN
      VFHZWRWNTWODVAWOWNGHXCWNEFRWNVASDWNVGVBVCABWRVDSVEVHUHUJVIVJVK $.
      $( [25-Nov-2005] $)

    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral2 $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( ( K - N
       ) ... ( K - M ) ) ph <-> A. k e. ( M ... N ) [ ( K - k ) / j ] ph ) ) $=
      ( cz wcel cmin co cfz wral wb w3a zsubcl 3adant2 3adant3 cc wceq zcn wsbc
      cv simp1 fzrevral syl3anc nncan oveq12d syl3an raleqdv bitrd 3coml ) DGHZ
      EGHZFGHZABDFIJZDEIJZKJLZABDCUBIJUAZCEFKJZLZMULUMUNNZUQURCDUPIJZDUOIJZKJZL
      ZUTVAUOGHZUPGHZULUQVEMULUNVFUMDFOPULUMVGUNDEOQULUMUNUCABCDUOUPUDUEVAURCVD
      USULDRHZUMERHZUNFRHZVDUSSDTETFTVHVIVJNVBEVCFKVHVIVBESVJDEUFQVHVJVCFSVIDFU
      FPUGUHUIUJUK $.
      $( [25-Nov-2005] $)

    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral3 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( A. j e. ( M ... N ) ph
                   <-> A. k e. ( M ... N ) [ ( ( M + N ) - k ) / j ] ph ) ) $=
      ( cz wcel wa cfz co wral caddc cv cmin wsbc wb zaddcl fzrevral cc zcn
      mpd3an3 wceq pncan pncan2 oveq12d syl2an raleqdv bitrd ) DFGZEFGZHZABDEIJ
      ZKZABDELJZCMNJOZCUNENJZUNDNJZIJZKZUOCULKUIUJUNFGUMUSPDEQABCUNDERUAUKUOCUR
      ULUIDSGZESGZURULUBUJDTETUTVAHUPDUQEIDEUCDEUDUEUFUGUH $.
      $( [11-Jul-2006] $) $( [20-Nov-2005] $)

    $( Shift the scanning order inside of a quantification over a finite set of
       sequential integers. $)
    fzshftral $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( M ... N
       ) ph <-> A. k e. ( ( M + K ) ... ( N + K ) ) [ ( k - K ) / j ] ph ) ) $=
      ( vx cz wcel cfz co wral cc0 cmin wsbc caddc wb cvv cc wceq w3a cv mp3an3
      0z fzrevral 3adant3 zsubcl mpan id syl3an 3com12 wal ax-gen oveq2 sbcco3g
      ovex mp2an ralbii zcn wa cneg df-neg oveq2i subneg addcom syl5eqr 3adant2
      eqtrd oveq12d 3coml raleqdv syl negsubdi2 syl2an dfsbcq ralbidva 3ad2ant3
      elfzelz bitrd syl5bb 3bitrd ) EHIZFHIZDHIZUAZABEFJKLZABMGUBZNKZOZGMFNKZME
      NKZJKLZWIGDCUBZNKZOZCDWKNKZDWJNKZJKZLZABWMDNKZOZCEDPKZFDPKZJKZLZWBWCWFWLQ
      ZWDWBWCMHIZXFUDABGMEFUEUCUFWCWBWDWLWSQZWCWJHIZWBWKHIZWDWDXHXGWCXIUDMFUGUH
      XGWBXJUDMEUGUHWDUIWIGCDWJWKUEUJUKWSABMWNNKZOZCWRLZWEXEWOXLCWRWNRIWHRIZGUL
      WOXLQDWMNUPXNGMWGNUPUMAGBWNWHXKRRWGWNMNUNUOUQURWEXMXLCXDLZXEWEXLCWRXDWBES
      IZWCFSIZWDDSIZWRXDTZEUSFUSDUSZXRXPXQXSXRXPXQUAWPXBWQXCJXRXPWPXBTXQXRXPUTZ
      WPDEVAZNKZXBYBWKDNEVBVCYAYCDEPKXBDEVDDEVEVHVFUFXRXQWQXCTXPXRXQUTZWQDFVAZN
      KZXCYEWJDNFVBVCYDYFDFPKXCDFVDDFVEVHVFVGVIVJUJVKWDWBXOXEQWCWDXLXACXDWDWMXD
      IZUTXKWTTZXLXAQWDXRWMSIZYHYGXTYGWMHIYIWMXBXCVRWMUSVLXRYIUTXKWNVAWTWNVBDWM
      VMVFVNABXKWTVOVLVPVQVSVTWA $.
      $( [19-Jun-2007] $) $( [27-Nov-2005] $)
  $}

  ${
    $d j k m n x y F $.  $d j k m n x y M $.  $d j k n x y N $.
    $( The values of a finite real sequence have an upper bound. $)
    fsequb $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N ) ( F ` k ) e. RR )
                 -> E. x e. RR A. k e. ( M ... N ) ( F ` k ) < x ) $=
      ( vn cv cr wcel cfz co wral clt wbr wrex wceq fveq2 wi raleqdv wa cfv cuz
      vj vm vy eleq1d cbvralv c1 caddc oveq2 rexbidv imbi12d cz rcla4v peano2re
      elfz3 syl elfz1eq ltp1 breq1d syl5ibrcom ralrimiv jca a1i ralbidv rcla4ev
      syl5 breq2 syl6 syld fzssp1 sseld ralimdv2 imp32 peano2uz eluzfz2 rcla4va
      imim1d sylan adantrl cle cif elfzp1 ad3antrrr max2 ancoms ad2antlr fzelp1
      wo wb an32s adantlr simplrl sylan2 ltletr syl3anc mpan2d ex a2d imp simpr
      ifcl adantl max1ALT ltletrd jaod sylbid jctild exp32 com34 rexlimdv imp3a
      cbvrexv syl6ib mp2and uzind4 sylan2b ) BGZCUAZHIZBDEJKZLEDUBUAZIZFGZCUAZH
      IZFYALZXSAGZMNZBYALZAHOZXTYFBFYAXRYDPXSYEHXRYDCQUFUGYCYGYKYFFDUCGZJKZLZYI
      BYMLZAHOZRYFFDDJKZLZYIBYQLZAHOZRYFFDUDGZJKZLZYIBUUBLZAHOZRZYFFDUUAUHUIKZJ
      KZLZYIBUUHLZAHOZRYGYKRUCUDDEYLDPZYNYRYPYTUULYFFYMYQYLDDJUJZSUULYOYSAHUULY
      IBYMYQUUMSUKULYLUUAPZYNUUCYPUUEUUNYFFYMUUBYLUUADJUJZSUUNYOUUDAHUUNYIBYMUU
      BUUOSUKULYLUUGPZYNUUIYPUUKUUPYFFYMUUHYLUUGDJUJZSUUPYOUUJAHUUPYIBYMUUHUUQS
      UKULYLEPZYNYGYPYKUURYFFYMYAYLEDJUJZSUURYOYJAHUURYIBYMYAUUSSUKULDUMIZYRDCU
      AZHIZYTUUTDYQIYRUVBRDUPYFUVBFDYQYDDPYEUVAHYDDCQUFUNUQUUTUVBUVAUHUIKZHIZXS
      UVCMNZBYQLZTZYTUVBUVGRUUTUVBUVDUVFUVAUOUVBUVEBYQXRYQIXRDPZUVBUVEXRDURUVBU
      VEUVHUVAUVCMNUVAUSUVHXSUVAUVCMXRDCQUTVAVGVBVCVDYSUVFAUVCHYHUVCPYIUVEBYQYH
      UVCXSMVHVEVFVIVJUUAYBIZUUFUUIUUKUVIUUFUUITTUUEUUGCUAZHIZUUKUVIUUFUUIUUEUV
      IUUIUUCUUEUVIYFYFFUUHUUBUVIYDUUBIYDUUHIYFUVIUUBUUHYDDUUAYBVKVLVRVMVRVNUVI
      UUIUVKUUFUVIUUGUUHIZUUIUVKUVIUUGYBIUVLDUUAVODUUGVPUQYFUVKFUUGUUHYDUUGPYEU
      VJHYDUUGCQUFVQVSVTUVIUUIUUEUVKTZUUKRUUFUVIUUITZUVMXSUEGZMNZBUUHLZUEHOZUUK
      UVNUUEUVKUVRUVNUUDUVKUVRRAHUVNYHHIZUVKUUDUVRUVNUVSUVKUUDUVRRUVNUVSUVKTZTZ
      UUDUVJUHUIKZYHWANZYHUWBWBZHIZXSUWDMNZBUUHLZTUVRUWAUUDUWGUWEUWAYIUWFBUUBUU
      HUWAXRUUBIZYIRZXRUUHIZUWFRUWAUWITZUWJUWHXRUUGPZWIZUWFUVIUWJUWMWJUUIUVTUWI
      XRDUUAWCWDUWKUWHUWFUWLUWAUWIUWHUWFRUWAUWHYIUWFUWAUWHYIUWFRUWAUWHTZYIYHUWD
      WANZUWFUVTUWOUVNUWHUVKUVSUWOUVKUWBHIZUVSUWOUVJUOZUWBYHWEVSWFWGUWNXTUVSUWE
      YIUWOTUWFRUVNUWHXTUVTUVIUWHUUIXTUVIUWHTUWJUUIXTYBXRDUUAWHYFXTFXRUUHYDXRPY
      EXSHYDXRCQUFVQVSWKWLUVNUVSUVKUWHWMUVTUWEUVNUWHUVKUVSUWPUWEUWQUWCYHUWBHXBW
      NZWGXSYHUWDWOWPWQWRWSWTUVTUWLUWFRUVNUWIUVTUWFUWLUVJUWDMNUVTUVJUWBUWDUVSUV
      KXAUVKUWPUVSUWQXCUWRUVKUVJUWBMNUVSUVJUSXCUVKUWBUWDWANZUVSUVKUWPUWSUWQUWBY
      HXDUQXCXEUWLXSUVJUWDMXRUUGCQUTVAWGXFXGWRVMUVTUWEUVNUWRXCXHUVQUWGUEUWDHUVO
      UWDPUVPUWFBUUHUVOUWDXSMVHVEVFVIXIXJXKXLUVQUUJUEAHUVOYHPUVPYIBUUHUVOYHXSMV
      HVEXMXNVTXOXIXPWTXQ $.
      $( [19-Sep-2005] $)

    $( The values of a finite real sequence have an upper bound. $)
    fsequb2 $p |- ( ( N e. ( ZZ>= ` M ) /\ F : ( M ... N ) --> RR ) ->
                  E. x e. RR A. y e. ran F y <_ x ) $=
      ( vk cuz cfv wcel cfz co cr wa cv clt wbr wral wrex wi adantlr wf cle crn
      ffvelrn ralrimiva fsequb sylan2 wceq wfn wb ffn fvelrnb syl biimpa biimpd
      reximi r19.36av 3syl adantll frn sselda simplr ltle syl2anc adantlll syld
      breq1 ralrimdva reximdva mpd ) EDGHIZDEJKZLCUAZMZFNZCHZANZOPZFVLQZALRZBNZ
      VQUBPZBCUCZQZALRVMVKVPLIZFVLQVTVMWEFVLVLLVOCUDUEAFCDEUFUGVNVSWDALVNVQLIZM
      ZVSWBBWCWGWAWCIZMVSWAVQOPZWBVNWHVSWISZWFVMWHWJVKVMWHMVPWAUHZFVLRZVRWISZFV
      LRWJVMWHWLVMCVLUIWHWLUJVLLCUKFVLWACULUMUNWKWMFVLWKVRWIVPWAVQOVGUOUPVRWIFV
      LUQURUSTVMWFWHWIWBSZVKVMWFMWHMWALIZWFWNVMWHWOWFVMWCLWAVLLCUTVATVMWFWHVBWA
      VQVCVDVEVFVHVIVJ $.
      $( [15-Oct-2006] $) $( [20-Sep-2005] $)

    $( The values of a finite real sequence have a supremum. $)
    fseqsupcl $p |- ( ( N e. ( ZZ>= ` M ) /\ F : ( M ... N ) --> RR )
                 -> sup ( ran F , RR , < ) e. RR ) $=
      ( vy vx cuz cfv wcel cfz co cr wf wa crn wss c0 wne cv cle wceq wral wrex
      wbr clt csup frn adantl cdm eluzfz1 fdm ne0i dm0rn0 eqeq1 syl5bir necon3d
      biimpd mpan9 syl2an fsequb2 suprcl syl3anc ) CBFGHZBCIJZKALZMANZKOZVEPQZD
      RERSUCDVEUAEKUBVEKUDUEKHVDVFVBVCKAUFUGVBBVCHZAUHZVCTZVGVDBCUIVCKAUJVHVCPQ
      VJVGVCBUKVJVEPVCPVEPTVIPTZVJVCPTZAULVJVKVLVIVCPUMUPUNUOUQUREDABCUSEDVEUTV
      A $.
      $( [9-Jul-2007] $) $( [20-Sep-2005] $)

    fseqsupub.1 $e |- N e. _V $.
    $( The values of a finite real sequence are bounded by their supremum. $)
    fseqsupubi $p |- ( ( K e. ( M ... N ) /\ F : ( M ... N ) --> RR ) ->
               ( F ` K ) <_ sup ( ran F , RR , < ) ) $=
      ( vy vx cfz co wcel cr c0 wne cv cle wbr cfv wceq sylan2 cvv crn wss wral
      wf wa wrex clt csup frn adantl cdm fdm ne0i dm0rn0 biimpd syl5bir necon3d
      eqeq1 mpan9 cuz elfzuz2 mpan fsequb2 sylan wfn ffn fnfvelrn ancoms suprub
      syl31anc ) BCDHIZJZVKKAUDZUEAUAZKUBZVNLMZFNGNOPFVNUCGKUFZBAQZVNJZVRVNKUGU
      HOPVMVOVLVKKAUIUJVMVLAUKZVKRZVPVKKAULVLVKLMWAVPVKBUMWAVNLVKLVNLRVTLRZWAVK
      LRZAUNWAWBWCVTVKLURUOUPUQUSSVLDCUTQJZVMVQDTJVLWDETBCDVAVBGFACDVCVDVMVLAVK
      VEZVSVKKAVFWEVLVSVKBAVGVHSGFVNVRVIVJ $.
      $( [29-Jul-2007] $) $( [20-Sep-2005] $)
  $}

