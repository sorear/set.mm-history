$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Positive reals (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of positive reals.  Definition of positive numbers in
     [Apostol] p. 20. $)
  df-rp $a |- RR+ = { x e. RR | 0 < x } $.

  ${
    $d x A $.
    $( Membership in the set of positive reals. $)
    elrp $p |- ( A e. RR+ <-> ( A e. RR /\ 0 < A ) ) $=
      ( vx cc0 cv clt wbr cr crp breq2 df-rp elrab2 ) CBDZEFCAEFBAGHLACEIBJK $.
      $( [27-Oct-2007] $)
  $}

  ${
    elrpi.1 $e |- A e. RR $.
    elrpi.2 $e |- 0 < A $.
    $( Membership in the set of positive reals. $)
    elrpii $p |- A e. RR+ $=
      ( crp wcel cr cc0 clt wbr elrp mpbir2an ) ADEAFEGAHIBCAJK $.
      $( [23-Feb-2008] $)
  $}

  $( 1 is a positive real.  (Contributed by Jeffrey Hankins, 23-Nov-2008.) $)
  1rp $p |- 1 e. RR+ $=
    ( c1 1re lt01 elrpii ) ABCD $.
    $( [23-Nov-2008] $)

  $( A positive real is a real. $)
  rpre $p |- ( A e. RR+ -> A e. RR ) $=
    ( vx crp cr cc0 cv clt wbr crab df-rp ssrab2 eqsstri sseli ) CDACEBFGHZBDID
    BJNBDKLM $.
    $( [27-Oct-2007] $)

  $( A positive real is a complex number. $)
  rpcn $p |- ( A e. RR+ -> A e. CC ) $=
    ( crp wcel rpre recnd ) ABCAADE $.
    $( [11-Nov-2008] $)

  $( A natural number is a positive real. $)
  nnrp $p |- ( A e. NN -> A e. RR+ ) $=
    ( cn wcel cr cc0 clt wbr crp nnre nngt0 elrp sylanbrc ) ABCADCEAFGAHCAIAJAK
    L $.
    $( [28-Nov-2008] $)

  $( The positive reals are a subset of the reals. $)
  rpssre $p |- RR+ C_ RR $=
    ( vx crp cr cv rpre ssriv ) ABCADEF $.
    $( [24-Feb-2008] $)

  $( A positive real is greater than zero.  (Contributed by FL,
     27-Dec-2007.) $)
  rpgt0 $p |- ( A e. RR+ -> 0 < A ) $=
    ( crp wcel cr cc0 clt wbr elrp simprbi ) ABCADCEAFGAHI $.
    $( [27-Dec-2007] $)

  $( A positive real is greater than or equal to zero. $)
  rpge0 $p |- ( A e. RR+ -> 0 <_ A ) $=
    ( crp wcel cr cc0 clt wbr cle rpre rpgt0 wi 0re ltle mpan sylc ) ABCADCZEAF
    GZEAHGZAIAJEDCPQRKLEAMNO $.
    $( [22-Feb-2008] $)

  $( A positive real is a positive real number. $)
  rpregt0 $p |- ( A e. RR+ -> ( A e. RR /\ 0 < A ) ) $=
    ( crp wcel cr cc0 clt wbr wa elrp biimpi ) ABCADCEAFGHAIJ $.
    $( [31-Jan-2014] $) $( [11-Nov-2008] $)

  $( A positive real is a nonnegative real number.  (Contributed by Mario
     Carneiro, 31-Jan-2014.) $)
  rprege0 $p |- ( A e. RR+ -> ( A e. RR /\ 0 <_ A ) ) $=
    ( crp wcel cr cc0 cle wbr rpre rpge0 jca ) ABCADCEAFGAHAIJ $.
    $( [31-Jan-2014] $)

  $( A positive real is nonzero. $)
  rpne0 $p |- ( A e. RR+ -> A =/= 0 ) $=
    ( crp wcel cr cc0 clt wbr wa wne rpregt0 gt0ne0 syl ) ABCADCEAFGHAEIAJAKL
    $.
    $( [18-Jul-2008] $)

  $( A positive real is a nonzero real number. $)
  rprene0 $p |- ( A e. RR+ -> ( A e. RR /\ A =/= 0 ) ) $=
    ( crp wcel cr cc0 wne rpre rpne0 jca ) ABCADCAEFAGAHI $.
    $( [11-Nov-2008] $)

  $( A positive real is a nonzero complex number. $)
  rpcnne0 $p |- ( A e. RR+ -> ( A e. CC /\ A =/= 0 ) ) $=
    ( crp wcel cc cc0 wne rpcn rpne0 jca ) ABCADCAEFAGAHI $.
    $( [11-Nov-2008] $)

  $( Quantification over positive reals. $)
  ralrp $p |- ( A. x e. RR+ ph <-> A. x e. RR ( 0 < x -> ph ) ) $=
    ( cc0 cv clt wbr wi crp cr wcel wa elrp imbi1i impexp bitri ralbii2 ) ACBDZ
    EFZAGZBHIQHJZAGQIJZRKZAGUASGTUBAQLMUARANOP $.
    $( [12-Feb-2008] $)

  $( Quantification over positive reals. $)
  rexrp $p |- ( E. x e. RR+ ph <-> E. x e. RR ( 0 < x /\ ph ) ) $=
    ( cc0 cv clt wbr wa crp cr wcel elrp anbi1i anass bitri rexbii2 ) ACBDZEFZA
    GZBHIPHJZAGPIJZQGZAGTRGSUAAPKLTQAMNO $.
    $( [21-May-2014] $)

  $( Closure law for addition of positive reals.  Part of Axiom 7 of [Apostol]
     p. 20. $)
  rpaddcl $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A + B ) e. RR+ ) $=
    ( crp wcel wa caddc co cr cc0 clt wbr rpre readdcl syl2an elrp an4s syl2anb
    addgt0 sylanbrc ) ACDZBCDZEABFGZHDZIUBJKZUBCDTAHDZBHDZUCUAALBLABMNTUEIAJKZE
    UFIBJKZEUDUAAOBOUEUFUGUHUDABRPQUBOS $.
    $( [27-Oct-2007] $)

  $( Closure law for multiplication of positive reals.  Part of Axiom 7 of
     [Apostol] p. 20. $)
  rpmulcl $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A x. B ) e. RR+ ) $=
    ( crp wcel wa cmul co cc0 clt wbr rpre remulcl syl2an elrp syl2anb sylanbrc
    cr mulgt0 ) ACDZBCDZEABFGZQDZHUAIJZUACDSAQDZBQDZUBTAKBKABLMSUDHAIJEUEHBIJEU
    CTANBNABROUANP $.
    $( [27-Oct-2007] $)

  $( Closure law for division of positive reals.  (Contributed by FL,
     27-Dec-2007.) $)
  rpdivcl $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A / B ) e. RR+ ) $=
    ( crp wcel wa cdiv co cr cc0 clt wbr rpre rprene0 redivcl 3expb syl2an elrp
    wne divgt0 syl2anb sylanbrc ) ACDZBCDZEABFGZHDZIUDJKZUDCDUBAHDZBHDZBIRZEUEU
    CALBMUGUHUIUEABNOPUBUGIAJKEUHIBJKEUFUCAQBQABSTUDQUA $.
    $( [27-Dec-2007] $)

  $( Closure law for reciprocation of positive reals.  (Contributed by Jeffrey
     Hankins, 23-Nov-2008.) $)
  rpreccl $p |- ( A e. RR+ -> ( 1 / A ) e. RR+ ) $=
    ( c1 crp wcel cdiv co 1rp rpdivcl mpan ) BCDACDBAEFCDGBAHI $.
    $( [23-Nov-2008] $)

  $( Closure law for half of a positive real.  (Contributed by Mario Carneiro,
     31-Jan-2014.) $)
  rphalfcl $p |- ( A e. RR+ -> ( A / 2 ) e. RR+ ) $=
    ( crp wcel c2 cdiv co 2re 2pos elrpii rpdivcl mpan2 ) ABCDBCADEFBCDGHIADJK
    $.
    $( [31-Jan-2014] $)

  $( Half of a positive real is less than the original number. $)
  rphalflt $p |- ( A e. RR+ -> ( A / 2 ) < A ) $=
    ( crp wcel cr cc0 clt wbr wa c2 cdiv co elrp halfpos biimpa sylbi ) ABCADCZ
    EAFGZHAIJKAFGZALPQRAMNO $.
    $( [21-May-2014] $)

  $( Closure law for division of a real by a positive real. $)
  rerpdivcl $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A / B ) e. RR ) $=
    ( crp wcel cr cc0 wne wa cdiv co rprene0 redivcl 3expb sylan2 ) BCDAEDZBEDZ
    BFGZHABIJEDZBKOPQRABLMN $.
    $( [10-Nov-2008] $)

  $( Either a nonzero real or its negation is a positive real, but not both.
     Axiom 8 of [Apostol] p. 20. $)
  rpneg $p |- ( ( A e. RR /\ A =/= 0 ) -> ( A e. RR+ <-> -. -u A e. RR+ ) ) $=
    ( cr wcel cc0 wne wa clt wbr cneg wn crp wo wi 0re mpan imp adantr wb bitrd
    elrp ltle olcd renegcl pm2.24d ltlen biimprd exp3acom23 jaod jctild impbid2
    cle simpl lenlt lt0neg1 notbid orbi2d ianor syl6bbr notbii 3bitr4g ) ABCZAD
    EZFZVADAGHZFZAIZBCZDVFGHZFZJZAKCVFKCZJVCVEVGJZVHJZLZVJVCVEVLDAUKHZLZVNVCVEV
    PVEVOVLVAVDVODBCZVAVDVOMNDAUAOPUBVCVPVDVAVCVLVDVOVAVLVDMVBVAVGVDAUCUDQVAVBV
    OVDMVAVOVBVDVAVDVOVBFZVQVAVDVRRNDAUEOUFUGPUHVAVBULUIUJVCVOVMVLVAVOVMRVBVAVO
    ADGHZJZVMVQVAVOVTRNDAUMOVAVSVHAUNUOSQUPSVGVHUQURATVKVIVFTUSUT $.
    $( [7-Nov-2008] $)

  $( Zero is not a positive real.  Axiom 9 of [Apostol] p. 20. $)
  0nrp $p |- -. 0 e. RR+ $=
    ( cc0 crp wcel clt wbr 0re ltnri rpgt0 mto ) ABCAADEAFGAHI $.
    $( [27-Oct-2007] $)

  $( Subtracting a positive real from another number decreases it.
     (Contributed by FL, 27-Dec-2007.) $)
  ltsubrp $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A - B ) < A ) $=
    ( crp wcel cr cc0 clt wbr wa cmin co elrp wi ltsubpos biimpd expcom sylan2b
    imp32 ) BCDAEDZBEDZFBGHZIABJKAGHZBLSTUAUBTSUAUBMTSIUAUBBANOPRQ $.
    $( [27-Dec-2007] $)

  $( Adding a positive number to another number increases it.  (Contributed by
     FL, 27-Dec-2007.) $)
  ltaddrp $p |- ( ( A e. RR /\ B e. RR+ ) -> A < ( A + B ) ) $=
    ( crp wcel cr cc0 clt wbr wa caddc co elrp wi ltaddpos biimpd imp32 sylan2b
    expcom ) BCDAEDZBEDZFBGHZIAABJKGHZBLSTUAUBTSUAUBMTSIUAUBBANORPQ $.
    $( [27-Dec-2007] $)

  $( Two ways to say one number is less than another. $)
  difrp $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A < B <-> ( B - A ) e. RR+ ) ) $=
    ( cr wcel wa clt wbr cc0 cmin co crp posdif wb resubcl ancoms elrp baib syl
    bitr4d ) ACDZBCDZEZABFGHBAIJZFGZUCKDZABLUBUCCDZUEUDMUATUFBANOUEUFUDUCPQRS
    $.
    $( [21-May-2014] $)

  ${
    $d x y A $.  $d x y B $.
    $( Show that ` A ` is less than ` B ` by showing that there is no positive
       bound on the difference.  (Contributed by Mario Carneiro,
       12-Jun-2014.) $)
    xralrple $p |- ( ( A e. RR* /\ B e. RR ) ->
                   ( A <_ B <-> A. x e. RR+ A <_ ( B + x ) ) ) $=
      ( vy cxr wcel cr wa cle wbr caddc co crp adantl wb syl2anc mpbid rexr syl
      wn cv wral cc0 rpge0 simplr rpre addge01 wi simpll readdcl xrletr syl3anc
      mpan2d ralrimdva cq wrex simpl qbtwnxr 3expia cmin simprrl ad2antrl difrp
      clt qre simprrr xrltnle cc wceq recnd pncan3 breq2d mtbird notbid rcla4ev
      oveq2 rexnal sylib expr rexlimdva con2d xrlenlt sylan2 sylibrd impbid
      syld ) BEFZCGFZHZBCIJZBCAUAZKLZIJZAMUBZWIWJWMAMWIWKMFZHZWJCWLIJZWMWPUCWKI
      JZWQWOWRWIWKUDNWPWHWKGFZWRWQOWGWHWOUEZWOWSWIWKUFNZCWKUGPQWPWGCEFZWLEFZWJW
      QHWMUHWGWHWOUIWPWHXBWTCRZSWPWLGFZXCWPWHWSXEWTXACWKUJPWLRSBCWLUKULUMUNWIWN
      CBVDJZTZWJWIXFWNWIXFCDUAZVDJZXHBVDJZHZDUOUPZWNTZWIXBWGXFXLUHWHXBWGXDNWGWH
      UQXBWGXFXLDCBURUSPWIXKXMDUOWIXHUOFZXKXMWIXNXKHZHZWMTZAMUPZXMXPXHCUTLZMFZB
      CXSKLZIJZTZXRXPXIXTWIXNXIXJVAXPWHXHGFZXIXTOWGWHXOUEZXNYDWIXKXHVEVBZCXHVCP
      QXPYBBXHIJZXPXJYGTZWIXNXIXJVFXPXHEFZWGXJYHOXPYDYIYFXHRSWGWHXOUIXHBVGPQXPY
      AXHBIXPCVHFXHVHFYAXHVIXPCYEVJXPXHYFVJCXHVKPVLVMXQYCAXSMWKXSVIZWMYBYJWLYAB
      IWKXSCKVPVLVNVOPWMAMVQVRVSVTWFWAWHWGXBWJXGOXDBCWBWCWDWE $.
      $( [12-Jun-2014] $)

    $( Show that ` A ` is less than ` B ` by showing that there is no positive
       bound on the difference.  (Contributed by Mario Carneiro,
       12-Jun-2014.) $)
    alrple $p |- ( ( A e. RR /\ B e. RR ) ->
                   ( A <_ B <-> A. x e. RR+ A <_ ( B + x ) ) ) $=
      ( cr wcel cxr cle wbr cv caddc co crp wral wb rexr xralrple sylan ) BDEBF
      ECDEBCGHBCAIJKGHALMNBOABCPQ $.
      $( [12-Jun-2014] $)
  $}

