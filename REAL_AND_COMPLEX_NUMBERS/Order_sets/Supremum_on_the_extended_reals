$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Supremum on the extended reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Adding minus infinity to a set does not affect the existence of its
       supremum. $)
    xrsupexmnf $p |- ( E. x e. RR* ( A. y e. A -. x < y /\
              A. y e. RR* ( y < x -> E. z e. A y < z ) ) ->
         E. x e. RR* ( A. y e. ( A u. { -oo } ) -. x < y /\
              A. y e. RR* ( y < x -> E. z e. ( A u. { -oo } ) y < z ) ) ) $=
      ( cv clt wbr wn wral wrex wi cxr wa cmnf csn cun wcel wo elun syl5bi wceq
      simpr elsn nltmnf breq2 notbid syl5ibrcom adantr ex ralimdv2 elun1 anim1i
      jaod reximi2 imim2i ralimi a1i anim12d reximia ) AEZBEZFGZHZBDIZVAUTFGZVA
      CEZFGZCDJZKZBLIZMVCBDNOZPZIZVEVGCVLJZKZBLIZMALUTLQZVDVMVJVPVQVCVCBDVLVQVA
      DQZVCKZVAVLQZVCKVTVRVAVKQZRVQVSMZVCVADVKSWBVRVCWAVQVSUBVQWAVCKVSWAVANUAZV
      QVCBNUCVQVCWCUTNFGZHUTUDWCVBWDVANUTFUEUFUGTUHUMTUIUJVJVPKVQVIVOBLVHVNVEVG
      VGCDVLVFDQVFVLQVGVFDVKUKULUNUOUPUQURUS $.
      $( [26-Oct-2005] $)

    $( Adding plus infinity to a set does not affect the existence of its
       infimum. $)
    xrinfmexpnf $p |- ( E. x e. RR* ( A. y e. A -. y < x /\
              A. y e. RR* ( x < y -> E. z e. A z < y ) ) ->
         E. x e. RR* ( A. y e. ( A u. { +oo } ) -. y < x /\
              A. y e. RR* ( x < y -> E. z e. ( A u. { +oo } ) z < y ) ) ) $=
      ( cv clt wbr wn wral wrex wi cxr wa cpnf csn cun wcel wo elun syl5bi wceq
      simpr elsn pnfnlt breq1 notbid syl5ibrcom adantr ex ralimdv2 elun1 anim1i
      jaod reximi2 imim2i ralimi a1i anim12d reximia ) BEZAEZFGZHZBDIZVAUTFGZCE
      ZUTFGZCDJZKZBLIZMVCBDNOZPZIZVEVGCVLJZKZBLIZMALVALQZVDVMVJVPVQVCVCBDVLVQUT
      DQZVCKZUTVLQZVCKVTVRUTVKQZRVQVSMZVCUTDVKSWBVRVCWAVQVSUBVQWAVCKVSWAUTNUAZV
      QVCBNUCVQVCWCNVAFGZHVAUDWCVBWDUTNVAFUEUFUGTUHUMTUIUJVJVPKVQVIVOBLVHVNVEVG
      VGCDVLVFDQVFVLQVGVFDVKUKULUNUOUPUQURUS $.
      $( [19-Jan-2006] $)

    $( Lemma for ~ xrsupss . $)
    xrsupsslem $p |- ( ( A C_ RR* /\ ( A C_ RR \/ +oo e. A ) ) -> E. x e. RR*
        ( A. y e. A -. x < y /\ A. y e. RR* ( y < x -> E. z e. A y < z ) ) ) $=
      ( cxr cr clt wbr wn wral wrex wi wa cpnf wcel c0 wceq cle cmnf breq1 sup3
      wss cv raleq rexeq imbi2d ralbidv anbi12d rexbidv wne rexr anim1i reximi2
      w3a syl elxr simpr pnfnlt adantr wb notbid adantl mpbird pm2.21d ad2antlr
      w3o ex wex ssel mnflt syl6 ancld eximdv n0 df-rex 3imtr4g imp a1d imbi12d
      ad2antrr 3jaod syl5bi ralimdv2 anim2d reximdva 3adant3 3expa ralnex ssel2
      mpd rexnal letric ord sylan an32s syl5bir ralimdva sylan2br breq2 cbvrexv
      ralbii sylib pnfxr ralrimiv c1 caddc peano2re rcla4va adantrr ancoms ltp1
      co sylan2 ancli ltletr mpand adantll exp31 a1dd com4r xrltnr ax-mp mtbiri
      cc0 0re impcom syl5ibr exp3a 3jaoi sylbi com13 jca imbi1d rcla4ev sylancr
      mpan syldan adantlr pm2.61dan mnfxr ral0 nltmnf pm3.2i mp2an a1i pm2.61ne
      rgen anim12i jaodan ) DEUBZDFUBZAUCZBUCZGHZIZBDJZUUMUULGHZUUMCUCZGHZCDKZL
      ZBEJZMZAEKZNDOZUUKUVDUUJUUKUVDUUOBPJZUUQUUSCPKZLZBEJZMZAEKZDPDPQZUVCUVJAE
      UVLUUPUVFUVBUVIUUOBDPUDUVLUVAUVHBEUVLUUTUVGUUQUUSCDPUEUFUGUHUIUUKDPUJZMZU
      UMUULRHZBDJZAFKZUVDUUKUVMUVQUVDUUKUVMUVQUNZUUPUVABFJZMZAEKZUVDUVRUVTAFKUW
      AABCDUAUVTUVTAFEUULFOZUULEOZUVTUULUKULUMUOUUKUVMUWAUVDLUVQUVNUVTUVCAEUVNU
      WCMZUVSUVBUUPUWDUVAUVABFEUWDUUMFOZUVALZUUMEOZUVALUWGUWEUUMNQZUUMSQZVFZUWD
      UWFMZUVAUUMUPZUWKUWEUVAUWHUWIUWDUWFUQUWCUWHUVALUVNUWFUWCUWHUVAUWCUWHMZUUQ
      UUTUWMUUQIZNUULGHZIZUWCUWPUWHUULURUSUWHUWNUWPUTUWCUWHUUQUWOUUMNUULGTVAVBV
      CVDVGVEUWDUWIUVALUWFUWDUWIUVAUWDUWIMUVASUULGHZSUURGHZCDKZLZUVNUWTUWCUWIUV
      NUWSUWQUUKUVMUWSUUKUURDOZCVHUXAUWRMZCVHUVMUWSUUKUXAUXBCUUKUXAUWRUUKUXAUUR
      FOZUWRDFUURVIUURVJZVKVLVMCDVNUWRCDVOVPVQVRVTUWIUVAUWTUTUWDUWIUUQUWQUUTUWS
      UUMSUULGTUWIUUSUWRCDUUMSUURGTUIZVSVBVCVGUSWAWBVGWCWDWEWFWJWGUUKUVQIZUVDUV
      MUUKUXFUULUURRHZCDKZAFJZUVDUUKUXFMUULUUMRHZBDKZAFJZUXIUXFUUKUVPIZAFJZUXLU
      VPAFWHUUKUXNUXLUUKUXMUXKAFUXMUVOIZBDKUUKUWBMZUXKUVOBDWKUXPUXOUXJBDUUKUUMD
      OZUWBUXOUXJLZUUKUXQMUWEUWBUXRDFUUMWIUWEUWBMUVOUXJUUMUULWLWMWNWOWEWPWQVQWR
      UXKUXHAFUXJUXGBCDUUMUURUULRWSWTXAXBUUKUXIMZNEOZNUUMGHZIZBDJZUUMNGHZUUTLZB
      EJZMZUVDXCUXSUYCUYFUUKUYCUXIUUKUYBBDUUKUXQUWEUYBDFUUMVIUWEUWGUYBUUMUKUUMU
      RZUOVKXDUSUXSUYEBEUUKUXIUWGUYELUWGUXIUUKUYEUWGUWJUXIUUKUYELZLZUWLUWEUYJUW
      HUWIUXIUUKUYDUWEUUTUXIUUKUWEUUTLUYDUXIUUKUWEUUTUXIUUKMZUWEMUUMXEXFXLZUURR
      HZCDKZUUTUWEUYKUYLFOZUYNUUMXGZUYOUYKUYNUYOUXIUYNUUKUXHUYNAUYLFUULUYLQUXGU
      YMCDUULUYLUURRTUIXHXIXJXMUUKUWEUYNUUTLUXIUUKUWEMUYMUUSCDUUKUXAUWEUYMUUSLZ
      UUKUXAMZUXCUWEUYQDFUURWIZUWEUXCUYQUWEUXCMUUMUYLGHZUYMUUSUWEUYTUXCUUMXKUSU
      WEUWEUYOMUXCUYTUYMMUUSLZUWEUYOUYPXNUWEUYOUXCVUAUUMUYLUURXOWGWNXPXJWNWOWEX
      QWJXRXSXTUWHUYIUXIUWHUYEUUKUWHUYDUUTUWHUYDNNGHZUXTVUBIXCNYAYBUUMNNGTYCVDV
      RVRUWIUXIUUKUYEUWIUYKUUTUYDUYKUUTUWIUWSUXIYDUURRHZCDKZUUKUWSYDFOUXIVUDYEU
      XHVUDAYDFUULYDQUXGVUCCDUULYDUURRTUIXHYPUUKVUDUWSUUKVUCUWRCDUYRUWRVUCUYRUX
      CUWRUYSUXDUOVRWEYFWNUXEYGXSYHYIYJYKVQXDYLUVCUYGANEUULNQZUUPUYCUVBUYFVUEUU
      OUYBBDVUEUUNUYAUULNUUMGTVAUGVUEUVAUYEBEVUEUUQUYDUUTUULNUUMGWSYMUGUHYNZYOY
      QYRYSUVKUUKSEOSUUMGHZIZBPJZUUMSGHZUVGLZBEJZMZUVKYTVUIVULVUHBUUAVUKBEUWGVU
      JUVGUUMUUBVDUUGUUCUVJVUMASEUULSQZUVFVUIUVIVULVUNUUOVUHBPVUNUUNVUGUULSUUMG
      TVAUGVUNUVHVUKBEVUNUUQVUJUVGUULSUUMGWSYMUGUHYNUUDUUEUUFVBUUJUVEMUXTUYGUVD
      XCUUJUYCUVEUYFUUJUYBBDUUJUXQUWGUYBDEUUMVIUYHVKXDUVEUYEBEUVEUYEUWGUVEUYDUU
      TUUSUYDCNDUURNUUMGWSYNVGVRXDUUHVUFYOUUI $.
      $( [25-Oct-2005] $)

    $( Lemma for ~ xrinfmss . $)
    xrinfmsslem $p |- ( ( A C_ RR* /\ ( A C_ RR \/ -oo e. A ) ) -> E. x e. RR*
        ( A. y e. A -. y < x /\ A. y e. RR* ( x < y -> E. z e. A z < y ) ) ) $=
      ( cxr cr clt wbr wn wral wrex wi wa cmnf wcel c0 wceq cle cpnf breq2 rexr
      wss raleq rexeq imbi2d ralbidv anbi12d rexbidv wne w3a anim1i reximi2 syl
      cv infm3 w3o elxr simpr wex ssel ltpnf ancld eximdv n0 df-rex 3imtr4g imp
      syl6 a1d ad2antrr wb imbi12d adantl mpbird adantr nltmnf pm2.21d ad2antlr
      ex notbid 3jaod syl5bi ralimdv2 anim2d reximdva 3adant3 mpd ralnex rexnal
      3expa ssel2 letric ancoms ord sylan an32s syl5bir ralimdva sylan2br breq1
      cbvrexv ralbii sylib mnfxr ralrimiv cmin peano2rem rcla4va adantrr sylan2
      wo c1 co ltm1 ancri lelttr 3expb mpan2d adantll exp31 a1dd com4r cc0 mpan
      0re impcom syl5ibr exp3a xrltnr ax-mp mtbiri 3jaoi sylbi com13 jca imbi1d
      rcla4ev sylancr syldan adantlr pm2.61dan pnfxr ral0 pnfnlt rgen mp2an a1i
      pm3.2i pm2.61ne anim12i jaodan ) DEUBZDFUBZBUNZAUNZGHZIZBDJZUUOUUNGHZCUNZ
      UUNGHZCDKZLZBEJZMZAEKZNDOZUUMUVFUULUUMUVFUUQBPJZUUSUVACPKZLZBEJZMZAEKZDPD
      PQZUVEUVLAEUVNUURUVHUVDUVKUUQBDPUCUVNUVCUVJBEUVNUVBUVIUUSUVACDPUDUEUFUGUH
      UUMDPUIZMZUUOUUNRHZBDJZAFKZUVFUUMUVOUVSUVFUUMUVOUVSUJZUURUVCBFJZMZAEKZUVF
      UVTUWBAFKUWCABCDUOUWBUWBAFEUUOFOZUUOEOZUWBUUOUAUKULUMUUMUVOUWCUVFLUVSUVPU
      WBUVEAEUVPUWEMZUWAUVDUURUWFUVCUVCBFEUWFUUNFOZUVCLZUUNEOZUVCLUWIUWGUUNSQZU
      UNNQZUPZUWFUWHMZUVCUUNUQZUWMUWGUVCUWJUWKUWFUWHURUWFUWJUVCLUWHUWFUWJUVCUWF
      UWJMUVCUUOSGHZUUTSGHZCDKZLZUVPUWRUWEUWJUVPUWQUWOUUMUVOUWQUUMUUTDOZCUSUWSU
      WPMZCUSUVOUWQUUMUWSUWTCUUMUWSUWPUUMUWSUUTFOZUWPDFUUTUTUUTVAZVHVBVCCDVDUWP
      CDVEVFVGVIVJUWJUVCUWRVKUWFUWJUUSUWOUVBUWQUUNSUUOGTUWJUVAUWPCDUUNSUUTGTUHZ
      VLVMVNVSVOUWEUWKUVCLUVPUWHUWEUWKUVCUWEUWKMZUUSUVBUXDUUSIZUUONGHZIZUWEUXGU
      WKUUOVPVOUWKUXEUXGVKUWEUWKUUSUXFUUNNUUOGTVTVMVNVQVSVRWAWBVSWCWDWEWFWGWJUU
      MUVSIZUVFUVOUUMUXHUUTUUORHZCDKZAFJZUVFUUMUXHMUUNUUORHZBDKZAFJZUXKUXHUUMUV
      RIZAFJZUXNUVRAFWHUUMUXPUXNUUMUXOUXMAFUXOUVQIZBDKUUMUWDMZUXMUVQBDWIUXRUXQU
      XLBDUUMUUNDOZUWDUXQUXLLZUUMUXSMUWGUWDUXTDFUUNWKUWGUWDMUVQUXLUWDUWGUVQUXLX
      KUUOUUNWLWMWNWOWPWEWQWRVGWSUXMUXJAFUXLUXIBCDUUNUUTUUORWTXAXBXCUUMUXKMZNEO
      ZUUNNGHZIZBDJZNUUNGHZUVBLZBEJZMZUVFXDUYAUYEUYHUUMUYEUXKUUMUYDBDUUMUXSUWGU
      YDDFUUNUTUWGUWIUYDUUNUAUUNVPZUMVHXEVOUYAUYGBEUUMUXKUWIUYGLUWIUXKUUMUYGUWI
      UWLUXKUUMUYGLZLZUWNUWGUYLUWJUWKUXKUUMUYFUWGUVBUXKUUMUWGUVBLUYFUXKUUMUWGUV
      BUXKUUMMZUWGMUUTUUNXLXFXMZRHZCDKZUVBUWGUYMUYNFOZUYPUUNXGZUYQUYMUYPUYQUXKU
      YPUUMUXJUYPAUYNFUUOUYNQUXIUYOCDUUOUYNUUTRTUHXHXIWMXJUUMUWGUYPUVBLUXKUUMUW
      GMUYOUVACDUUMUWSUWGUYOUVALZUUMUWSMZUXAUWGUYSDFUUTWKZUXAUWGMUYOUYNUUNGHZUV
      AUWGVUBUXAUUNXNVMUWGUXAUYQUWGMUYOVUBMUVALZUWGUYQUYRXOUXAUYQUWGVUCUUTUYNUU
      NXPXQXJXRWOWPWEXSWGXTYAYBUWJUXKUUMUYGUWJUYMUVBUYFUYMUVBUWJUWQUXKUUTYCRHZC
      DKZUUMUWQYCFOUXKVUEYEUXJVUEAYCFUUOYCQUXIVUDCDUUOYCUUTRTUHXHYDUUMVUEUWQUUM
      VUDUWPCDUYTUWPVUDUYTUXAUWPVUAUXBUMVIWEYFWOUXCYGYAYHUWKUYKUXKUWKUYGUUMUWKU
      YFUVBUWKUYFNNGHZUYBVUFIXDNYIYJUUNNNGTYKVQVIVIYLYMYNVGXEYOUVEUYIANEUUONQZU
      URUYEUVDUYHVUGUUQUYDBDVUGUUPUYCUUONUUNGTVTUFVUGUVCUYGBEVUGUUSUYFUVBUUONUU
      NGWTYPUFUGYQZYRYSYTUUAUVMUUMSEOUUNSGHZIZBPJZSUUNGHZUVILZBEJZMZUVMUUBVUKVU
      NVUJBUUCVUMBEUWIVULUVIUUNUUDVQUUEUUHUVLVUOASEUUOSQZUVHVUKUVKVUNVUPUUQVUJB
      PVUPUUPVUIUUOSUUNGTVTUFVUPUVJVUMBEVUPUUSVULUVIUUOSUUNGWTYPUFUGYQUUFUUGUUI
      VMUULUVGMUYBUYIUVFXDUULUYEUVGUYHUULUYDBDUULUXSUWIUYDDEUUNUTUYJVHXEUVGUYGB
      EUVGUYGUWIUVGUYFUVBUVAUYFCNDUUTNUUNGWTYQVSVIXEUUJVUHYRUUK $.
      $( [19-Jan-2006] $)

    $( Any subset of extended reals has a supremum. $)
    xrsupss $p |- ( A C_ RR* -> E. x e. RR* (
         A. y e. A -. x < y /\ A. y e. RR* ( y < x -> E. z e. A y < z ) ) ) $=
      ( cxr wss cr cpnf wcel wo cmnf cv clt wbr wn wral wrex wi wa wceq w3o csn
      ssxr df-3or sylib xrsupsslem cdif ssdifss eqid wne eldifsn necon2bi ax-mp
      simprbi biorfi bitr4i mpdan adantr xrsupexmnf wb snssi undif uncom eqeq1i
      syl cun bitri raleq rexeq imbi2d ralbidv anbi12d sylbi rexbidv syl5ib mpd
      adantl jaodan ) DEFZDGFZHDIZJZKDIZJZALZBLZMNOZBDPZWFWEMNZWFCLMNZCDQZRZBEP
      ZSZAEQZVSVTWAWCUAWDDUCVTWAWCUDUEVSWBWOWCABCDUFVSWCSWGBDKUBZUGZPWIWJCWQQRB
      EPSAEQZWOVSWRWCVSWQEFZWRDEWPUHWSWQGFZHWQIZJZWRWSWTXAKWQIZUAZXBWQUCXDXBXCJ
      XBWTXAXCUDXCXBKKTXCOKUIXCKKXCWCKKUJKDKUKUNULUMUOUPUEABCWQUFUQVEURWCWRWORV
      SWRWGBWQWPVFZPZWIWJCXEQZRZBEPZSZAEQWCWOABCWQUSWCXJWNAEWCWPDFZXJWNUTZKDVAX
      KXEDTZXLXKWPWQVFZDTXMWPDVBXNXEDWPWQVCVDVGXMXFWHXIWMWGBXEDVHXMXHWLBEXMXGWK
      WIWJCXEDVIVJVKVLVMVEVNVOVQVPVRUQ $.
      $( [25-Oct-2005] $)

    $( Any subset of extended reals has an infimum. $)
    xrinfmss $p |- ( A C_ RR* -> E. x e. RR* (
         A. y e. A -. y < x /\ A. y e. RR* ( x < y -> E. z e. A z < y ) ) ) $=
      ( cxr wss cr cmnf wcel wo cpnf cv clt wbr wn wral wrex wi wa w3o ssxr csn
      df-3or or32 bitri sylib xrinfmsslem cdif ssdifss 3orass pnfxr snid elndif
      wb elexi ax-mp biorf orbi2i bitr4i mpdan syl adantr xrinfmexpnf wceq snss
      undif uncom eqeq1i 3bitri raleq rexeq imbi2d ralbidv anbi12d sylbi syl5ib
      cun rexbidv adantl mpd jaodan ) DEFZDGFZHDIZJZKDIZJZBLZALZMNOZBDPZWIWHMNZ
      CLWHMNZCDQZRZBEPZSZAEQZWBWCWFWDTZWGDUAWSWCWFJWDJWGWCWFWDUCWCWFWDUDUEUFWBW
      EWRWFABCDUGWBWFSWJBDKUBZUHZPWLWMCXAQRBEPSAEQZWRWBXBWFWBXAEFZXBDEWTUIXCXAG
      FZHXAIZJZXBXCXDKXAIZXETZXFXAUAXHXDXGXEJZJXFXDXGXEUJXEXIXDXGOZXEXIUNKWTIXJ
      KKEUKUOZULKWTDUMUPXGXEUQUPURUSUFABCXAUGUTVAVBWFXBWRRWBXBWJBXAWTVQZPZWLWMC
      XLQZRZBEPZSZAEQWFWRABCXAVCWFXQWQAEWFXLDVDZXQWQUNWFWTDFWTXAVQZDVDXRKDXKVEW
      TDVFXSXLDWTXAVGVHVIXRXMWKXPWPWJBXLDVJXRXOWOBEXRXNWNWLWMCXLDVKVLVMVNVOVRVP
      VSVTWAUT $.
      $( [25-Oct-2005] $)
    $( Any subset of extended reals has an infimum.  (Contributed by Mario
       Carneiro, 16-Mar-2014.) $)
    xrinfmss2 $p |- ( A C_ RR* -> E. x e. RR* ( A. y e. A -. x `' < y /\
                      A. y e. RR* ( y `' < x -> E. z e. A y `' < z ) ) ) $=
      ( cxr wss cv clt wbr wn wral wrex wi wa ccnv xrinfmss brcnv ralbii rexbii
      vex notbii imbi12i anbi12i sylibr ) DEFBGZAGZHIZJZBDKZUFUEHIZCGZUEHIZCDLZ
      MZBEKZNZAELUFUEHOZIZJZBDKZUEUFUQIZUEUKUQIZCDLZMZBEKZNZAELABCDPVFUPAEUTUIV
      EUOUSUHBDURUGUFUEHATZBTZQUARVDUNBEVAUJVCUMUEUFHVHVGQVBULCDUEUKHVHCTQSUBRU
      CSUD $.
      $( [16-Mar-2014] $)

    $( By quantifying only over reals, we can specify any extended real upper
       bound for any set of extended reals. $)
    xrub $p |- ( ( A C_ RR* /\ B e. RR* ) -> ( A. x e. RR ( x < B ->
          E. y e. A x < y ) <-> A. x e. RR* ( x < B -> E. y e. A x < y ) ) ) $=
      ( vz cxr wcel wa clt wbr wrex wi cr wceq breq1 rexbidv cpnf cmnf c1 ax-mp
      wss wral imbi12d cbvralv w3o elxr pm2.27 a1i pnfnlt notbid syl5ibr pm2.21
      cv wn syl6com ad2antlr a1dd cmin peano2rem rcla4v syl adantl ltm1 syl5com
      co id mnflt rexr ssel2 adantlr mnfxr xrlttr mp3an1 syl2anc mpand reximdva
      3syld ltpnf breq2 mpbiri mp3an12 mpani sylan9r syl5 xrltnr mtbiri pm2.21d
      1re a1d 3jaodan sylan2b syl5ibrcom 3jaod syl5bi com23 ralimdv2 ex pm2.43d
      imp imim1i ralimi2 impbid1 ) CFUAZDFGZHZAUMZDIJZXFBUMZIJZBCKZLZAMUBZXKAFU
      BZXEXLXMXLEUMZDIJZXNXHIJZBCKZLZEMUBZXEXLXMLZXKXRAEMXFXNNZXGXOXJXQXFXNDIOY
      AXIXPBCXFXNXHIOPUCUDXEXSXTXEXSHZXKXKAMFYBXFFGZXFMGZXKLZXKYCYDXFQNZXFRNZUE
      YBYEXKLZXFUFYBYDYHYFYGYDYHLYBYDXKUGUHYBYFXKYEXDYFXKLXCXSYFXDXGUNZXKXDYIYF
      QDIJZUNDUIYFXGYJXFQDIOUJUKXGXJULUOUPUQYBYGXKYEYBXKYGRDIJZRXHIJZBCKZLZXEXS
      YNXDXCDMGZDQNZDRNZUEXSYNLZDUFXCYOYRYPYQXCYOHZXSYMYKYSXSDSURVEZDIJZYTXHIJZ
      BCKZLZUUCYMYOXSUUDLZXCYOYTMGZUUEDUSZXRUUDEYTMXNYTNZXOUUAXQUUCXNYTDIOUUHXP
      UUBBCXNYTXHIOPUCUTVAVBYOUUDUUCLXCYOUUAUUDUUCDVCUUDVFVDVBYSUUBYLBCYSXHCGZH
      ZRYTIJZUUBYLUUJUUFUUKYOUUFXCUUIUUGUPZYTVGVAUUJYTFGZXHFGZUUKUUBHYLLZUUJUUF
      UUMUULYTVHVAXCUUIUUNYOCFXHVIZVJRFGZUUMUUNUUOVKRYTXHVLVMVNVOVPVQUQXCYPHZXS
      YMYKXSSDIJZSXHIJZBCKZLZUURYMSMGZXSUVBLWHXRUVBESMXNSNZXOUUSXQUVAXNSDIOUVDX
      PUUTBCXNSXHIOPUCUTTYPUVBUVAXCYMYPUUSUVBUVAYPUUSSQIJZUVCUVEWHSVRTDQSIVSVTU
      VBVFVDXCUUTYLBCXCUUIHUUNUUTYLLUUPUUNRSIJZUUTYLUVCUVFWHSVGTUUQSFGZUUNUVFUU
      THYLLVKUVCUVGWHSVHTRSXHVLWAWBVAVPWCWDUQXCYQHZYNXSUVHYKYMYQYKUNXCYQYKRRIJZ
      UUQUVIUNVKRWETDRRIVSWFVBWGWIWJWKWSYGXGYKXJYMXFRDIOYGXIYLBCXFRXHIOPUCWLUQW
      MWNWOWPWQWNWRXKXKAFMYDYCXKXFVHWTXAXB $.
      $( [9-Apr-2006] $)

    $( The supremum of a set of extended reals. $)
    supxr $p |- ( ( ( A C_ RR* /\ B e. RR* ) /\ ( A. x e. A -. B < x /\
    A. x e. RR ( x < B -> E. y e. A x < y ) ) ) -> sup ( A , RR* , < ) = B ) $=
      ( vz cxr wss wcel wa cv clt wbr wn wral wrex wi cr wceq anim2i ralbidv wb
      csup xrub biimpd anim2d crab cuni df-sup wreu xrsupss ancoms xrltso supeu
      imp breq1 notbid breq2 imbi1d anbi12d reuuni2 3syl biimpa syl5eq syldan )
      CFGZDFHZIZDAJZKLZMZACNZVHDKLZVHBJKLBCOZPZAQNZIZVKVNAFNZIZCFKUBZDRVGVPVRVG
      VOVQVKVGVOVQABCDUCUDUEUNVGVRIVSEJZVHKLZMZACNZVHVTKLZVMPZAFNZIZEFUFUGZDEAB
      CFKUHVGVRWHDRZVGVFWGEFOZIZVFWGEFUIZIVRWIUAVFVEWKVEWJVFEABCUJSUKWJWLVFEABF
      CKULUMSWGVREFDVTDRZWCVKWFVQWMWBVJACWMWAVIVTDVHKUOUPTWMWEVNAFWMWDVLVMVTDVH
      KUQURTUSUTVAVBVCVD $.
      $( [9-Apr-2006] $)

    $( The supremum of a set of extended reals. $)
    supxr2 $p |- ( ( ( A C_ RR* /\ B e. RR* ) /\ ( A. x e. A x <_ B /\
  A. x e. RR ( x < B -> E. y e. A x < y ) ) ) -> sup ( A , RR* , < ) = B ) $=
      ( cxr wss wcel wa cv cle wbr wral clt wrex wi cr wn csup wceq wb ralbidva
      ssel2 xrlenlt sylan an32s anbi1d biimpa supxr syldan ) CEFZDEGZHZAIZDJKZA
      CLZUMDMKUMBIMKBCNOAPLZHZDUMMKQZACLZUPHZCEMRDSULUQUTULUOUSUPULUNURACUJUMCG
      ZUKUNURTZUJVAHUMEGUKVBCEUMUBUMDUCUDUEUAUFUGABCDUHUI $.
      $( [9-Apr-2006] $)

    $( The supremum of an arbitrary set of extended reals is an extended
       real. $)
    supxrcl $p |- ( A C_ RR* -> sup ( A , RR* , < ) e. RR* ) $=
      ( vx vy vz cxr wss cv clt wbr wn wral wrex wi wa csup wcel xrsupss xrltso
      supcl syl ) AEFBGZCGZHIJCAKUBUAHIUBDGHIDALMCEKNBELAEHOEPBCDAQBCDEAHRST $.
      $( [24-Oct-2005] $)

    $( The supremum of the union of two sets of extended reals equals the
       largest of their suprema. $)
    supxrun $p |- ( ( A C_ RR* /\ B C_ RR* /\ sup ( A , RR* , < ) <_ sup
   ( B , RR* , < ) ) -> sup ( ( A u. B ) , RR* , < ) = sup ( B , RR* , < ) ) $=
      ( vx vy vz vw cxr wss clt csup wbr wcel cv wn wral wrex wi cr wa 3ad2ant2
      cle w3a cun wceq unss biimpi 3adant3 supxrcl wo elun xrsupss xrltso supub
      syl 3ad2ant1 ad2antrr ad2antlr ssel2 adantlr xrlelttr syl3anc con3d exp41
      expdimp com34 3imp mpdd jaod syl5bi ralrimiv suplub sylani anim1i reximi2
      rexr elun2 syl6 exp3a supxr syl22anc ) AGHZBGHZAGIJZBGIJZUAKZUBZABUCZGHZW
      DGLZWDCMZIKZNZCWGOWJWDIKZWJDMZIKZDWGPZQZCROZWGGIJWDUDWAWBWHWEWAWBSZWHABGU
      EUFUGWBWAWIWEBUHZTWFWLCWGWJWGLWJALZWJBLZUIWFWLWJABUJWFXAWLXBWFXAWCWJIKZNZ
      WLWAWBXAXDQZWEWAWNEMZIKNZEAOXFWNIKZXFFMIKZFAPQEGOSDGPXEDEFAUKDEFGAWJIULUM
      UNUOWAWBWEXAXDWLQZQWAWBXAWEXJWAWBXAWEXJWSXASZWESWKXCXKWEWKXCXKWCGLZWIWJGL
      ZWEWKSXCQWAXLWBXAAUHUPWBWIWAXAWTUQWAXAXMWBAGWJURUSWCWDWJUTVAVDVBVCVEVFVGW
      BWAXBWLQZWEWBXGEBOXHXIFBPQEGOSDGPXNDEFBUKDEFGBWJIULUMUNTVHVIVJWBWAWRWEWBW
      QCRWBWJRLZWMWPWBXOWMSZWODBPZWPWBWJXFIKNEBOXFWJIKXHDBPQEGOSCGPZXPXQQCEDBUK
      XOXRXMWMXQWJVOCEDGBWJIULVKVLUNWOWODBWGWNBLWNWGLWOWNBAVPVMVNVQVRVJTCDWGWDV
      SVT $.
      $( [19-Jan-2006] $)

    $( The infimum of an arbitrary set of extended reals is an extended
       real. $)
    infmxrcl $p |- ( A C_ RR* -> sup ( A , RR* , `' < ) e. RR* ) $=
      ( vx vy vz cxr wss cv clt ccnv wbr wn wral wrex wi wa csup wcel xrinfmss2
      wor xrltso cnvso mpbi supcl syl ) AEFBGZCGZHIZJKCALUFUEUGJUFDGUGJDAMNCELO
      BEMAEUGPEQBCDARBCDEAUGEHSEUGSTEHUAUBUCUD $.
      $( [16-Mar-2014] $) $( [19-Jan-2006] $)

    $( Adding minus infinity to a set does not affect its supremum. $)
    supxrmnf $p |- ( A C_ RR* ->
                 sup ( ( A u. { -oo } ) , RR* , < ) = sup ( A , RR* , < ) ) $=
      ( cxr wss cmnf csn cun clt csup uncom supeq1i wceq wcel mnfxr snssi ax-mp
      cle wbr a1i id xrltso supsn supxrcl mnfle syl5eqbr supxrun syl3anc syl5eq
      syl ) ABCZADEZFZBGHUJAFZBGHZABGHZBUKULGAUJIJUIUJBCZUIUJBGHZUNPQUMUNKUOUID
      BLZUOMDBNORUISUIUPDUNPUQUPDKMBDGTUAOUIUNBLDUNPQAUBUNUCUHUDUJAUEUFUG $.
      $( [19-Jan-2006] $)

    $( The supremum of a set of extended reals containing plus infnity is plus
       infinity. $)
    supxrpnf $p |- ( ( A C_ RR* /\ +oo e. A ) -> sup ( A , RR* , < ) = +oo ) $=
      ( vy vz cxr wss cpnf wcel cv clt wbr wn wral wrex wi cr wa csup wceq ssel
      ralrimiv pnfnlt syl6 breq2 rcla4ev a1d anim12i pnfxr supxr mpanl2 syldan
      ex ) ADEZFAGZFBHZIJKZBALZUNFIJZUNCHZIJZCAMZNZBOLZPZADIQFRZULUPUMVBULUOBAU
      LUNAGUNDGUOADUNSUNUAUBTUMVABOUMVAUNOGUMUQUTUSUQCFAURFUNIUCUDUKUETUFULFDGV
      CVDUGBCAFUHUIUJ $.
      $( [14-Dec-2006] $) $( [15-Oct-2005] $)

    $( The supremum of an unbounded-above set of extended reals is plus
       infinity. $)
    supxrunb1 $p |- ( A C_ RR* -> ( A. x e. RR E. y e. A x <_ y <->
                    sup ( A , RR* , < ) = +oo ) ) $=
      ( vz vw cxr cv cle wbr wrex cr wral clt cpnf wceq wn wi wa wcel rexr csup
      wss ssel pnfnlt syl6 ralrimiv adantr c1 caddc co peano2re rexbidv rcla4va
      breq1 adantrr ancoms sylan2 ssel2 ltp1 ancli xrltletr syl3an2 3expa sylan
      syl3an1 mpand an32s reximdva adantll mpd exp31 a1dd com4r com13 imp pnfxr
      jca supxr mpanl2 syldan ex xrsupss ad2antrr ad2antlr ltpnf syl5ibr impcom
      breq2 xrltso suplub sylc adantlr xrltle syl2anc syld ralrimdva impbid ) C
      FUBZAGZBGZHIZBCJZAKLZCFMUAZNOZWRXCXEWRXCNDGZMIPZDCLZXFNMIZXFWTMIZBCJZQZDK
      LZRZXEWRXCRZXHXMWRXHXCWRXGDCWRXFCSXFFSZXGCFXFUCXFUDUEUFUGXOXLDKWRXCXFKSZX
      LQXQXCWRXLXCWRXIXQXKXCWRXQXKQXIXCWRXQXKXCWRRZXQRXFUHUIUJZWTHIZBCJZXKXQXRX
      SKSZYAXFUKZYBXRYAYBXCYAWRXBYAAXSKWSXSOXAXTBCWSXSWTHUNULUMUOUPUQWRXQYAXKQX
      CWRXQRXTXJBCWRWTCSZXQXTXJQZWRYDRWTFSZXQYECFWTURZXQYFYEXQYFRXFXSMIZXTXJXQY
      HYFXFUSUGXQXQYBRYFYHXTRXJQZXQYBYCUTXQYBYFYIXQXPYBYFYIXFTYBXPXSFSYFYIXSTXF
      XSWTVAVBVEVCVDVFUPVDVGVHVIVJVKVLVMVNVOUFVQWRNFSXNXEVPDBCNVRVSVTWAWRXEXBAK
      WRWSKSZRZXEWSWTMIZBCJZXBYKXEYMYKXERZXFEGZMIPECLYOXFMIYOWTMIBCJQEFLRDFJZWS
      FSZWSXDMIZRYMWRYPYJXEDEBCWBWCYNYQYRYJYQWRXEWSTZWDYJXEYRWRXEYJYRYJYRXEWSNM
      IWSWEXDNWSMWHWFWGVIVQDEBFCWSMWIWJWKWAYKYLXABCYKYDRYQYFYLXAQYJYQWRYDYSWDWR
      YDYFYJYGWLWSWTWMWNVHWOWPWQ $.
      $( [19-Jan-2006] $)

    $( The supremum of an unbounded-above set of extended reals is plus
       infinity. $)
    supxrunb2 $p |- ( A C_ RR* -> ( A. x e. RR E. y e. A x < y <->
                    sup ( A , RR* , < ) = +oo ) ) $=
      ( vz vw cxr cv clt wbr wrex cr wral cpnf wceq wn wi wcel ralrimiv exp31
      wa csup ssel pnfnlt syl6 adantr breq1 rexbidv rcla4va adantrr ancoms a1dd
      wss com4r com13 imp jca pnfxr supxr mpanl2 xrsupss ad2antrr rexr ad2antlr
      syldan ex ltpnf breq2 syl5ibr impcom adantll xrltso suplub com23 ralrimdv
      sylc impbid ) CFULZAGZBGZHIZBCJZAKLZCFHUAZMNZVQWBWDVQWBMDGZHIOZDCLZWEMHIZ
      WEVSHIZBCJZPZDKLZTZWDVQWBTZWGWLVQWGWBVQWFDCVQWECQWEFQWFCFWEUBWEUCUDRUEWNW
      KDKVQWBWEKQZWKPWOWBVQWKWBVQWHWOWJWBVQWOWJPWHWBVQWOWJWOWBVQTWJWOWBWJVQWAWJ
      AWEKVRWENVTWIBCVRWEVSHUFUGUHUIUJSUKUMUNUORUPVQMFQWMWDUQDBCMURUSVDVEVQWDWA
      AKVQVRKQZWDWAVQWPWDWAVQWPTWDTZWEEGZHIOECLWRWEHIWRVSHIBCJPEFLTDFJZVRFQZVRW
      CHIZTWAVQWSWPWDDEBCUTVAWQWTXAWPWTVQWDVRVBVCWPWDXAVQWDWPXAWPXAWDVRMHIVRVFW
      CMVRHVGVHVIVJUPDEBFCVRHVKVLVOSVMVNVP $.
      $( [19-Jan-2006] $)

    $( The supremum of a bounded-above set of extended reals is less than
       infinity. $)
    supxrbnd1 $p |- ( A C_ RR* -> ( E. x e. RR A. y e. A y < x <->
                    sup ( A , RR* , < ) < +oo ) ) $=
      ( cxr wss cv clt wbr wral cr wrex csup cpnf wn cle wceq ralnex wcel wa wb
      ssel2 rexr xrlenlt ancoms syl2an rexbidva rexnal syl6rbb ralbidva syl5bbr
      an32s supxrunb1 supxrcl nltpnft syl 3bitrd con4bid ) CDEZBFZAFZGHZBCIZAJK
      ZCDGLZMGHZURVCNZUTUSOHZBCKZAJIZVDMPZVENZVFVBNZAJIURVIVBAJQURVLVHAJURUTJRZ
      SZVHVANZBCKVLVNVGVOBCURUSCRZVMVGVOTZURVPSUSDRZUTDRZVQVMCDUSUAUTUBVSVRVQUT
      USUCUDUEUKUFVABCUGUHUIUJABCULURVDDRVJVKTCUMVDUNUOUPUQ $.
      $( [13-Nov-2006] $) $( [30-Jan-2006] $)

    $( The supremum of a bounded-above set of extended reals is less than
       infinity. $)
    supxrbnd2 $p |- ( A C_ RR* -> ( E. x e. RR A. y e. A y <_ x <->
                    sup ( A , RR* , < ) < +oo ) ) $=
      ( cxr wss cv cle wbr wral cr wrex clt csup cpnf wn wceq ralnex wcel wa wb
      ssel2 rexr xrlenlt con2bid syl2an an32s rexbidva syl6rbb ralbidva syl5bbr
      rexnal supxrunb2 supxrcl nltpnft syl 3bitrd con4bid ) CDEZBFZAFZGHZBCIZAJ
      KZCDLMZNLHZURVCOZUTUSLHZBCKZAJIZVDNPZVEOZVFVBOZAJIURVIVBAJQURVLVHAJURUTJR
      ZSZVHVAOZBCKVLVNVGVOBCURUSCRZVMVGVOTZURVPSUSDRZUTDRZVQVMCDUSUAUTUBVRVSSVA
      VGUSUTUCUDUEUFUGVABCUKUHUIUJABCULURVDDRVJVKTCUMVDUNUOUPUQ $.
      $( [26-Jun-2007] $) $( [30-Jan-2006] $)

    $( The supremum of an empty set under the extended reals is minus
       infinity. $)
    xrsup0 $p |- sup ( (/) , RR* , < ) = -oo $=
      ( vy vz c0 cxr wss cmnf wcel cv clt wbr wn wral wrex wi cr csup 0ss mnfxr
      wceq ral0 rexr nltmnf syl pm2.21d rgen supxr mp4an ) CDEFDGFAHZIJKZACLUHF
      IJZUHBHIJBCMZNZAOLCDIPFSDQRUIATULAOUHOGZUJUKUMUHDGUJKUHUAUHUBUCUDUEABCFUF
      UG $.
      $( [15-Oct-2005] $)

    $( A member of a set of extended reals is less than or equal to the set's
       supremum. $)
    supxrub $p |- ( ( A C_ RR* /\ B e. A ) -> B <_ sup ( A , RR* , < ) ) $=
      ( vx vy vz cxr wss wcel wa clt csup cle wn cv wral wrex wi xrsupss xrltso
      wbr supub syl imp wb ssel2 supxrcl adantr xrlenlt syl2anc mpbird ) AFGZBA
      HZIZBAFJKZLTZUNBJTMZUKULUPUKCNZDNZJTMDAOURUQJTURENJTEAPQDFOICFPULUPQCDEAR
      CDEFABJSUAUBUCUMBFHUNFHZUOUPUDAFBUEUKUSULAUFUGBUNUHUIUJ $.
      $( [7-Feb-2006] $)

    $( The supremum of a set of extended reals is less than or equal to an
       upper bound.  (Revised by Mario Carneiro, 6-Sep-2014.) $)
    supxrleub $p |- ( ( A C_ RR* /\ B e. RR* ) ->
                      ( sup ( A , RR* , < ) <_ B <-> A. x e. A x <_ B ) ) $=
      ( vy vz cxr wss wcel wa clt csup wbr wn cv wral cle wrex wi wb xrlenlt
      xrsupss adantr simpl xrltso suplub2 syl3anc notbid ralnex syl6bbr supxrcl
      simpr sylan sselda simplr syl2anc ralbidva 3bitr4d ) BFGZCFHZIZCBFJKZJLZM
      ZCANZJLZMZABOZVACPLZVDCPLZABOUTVCVEABQZMVGUTVBVJUTDNZENZJLMEBOVLVKJLVLVDJ
      LABQREFOIDFQZURUSVBVJSURVMUSDEABUAUBURUSUCZURUSUKDEAFBCJUDUEUFUGVEABUHUIU
      RVAFHUSVHVCSBUJVACTULUTVIVFABUTVDBHZIVDFHUSVIVFSUTBFVDVNUMURUSVOUNVDCTUOU
      PUQ $.
      $( [6-Sep-2014] $) $( [22-Feb-2006] $)

    $( The real and extended real suprema match when the real supremum exists.
       (The proof was shortened by Mario Carneiro, 7-Sep-2014.) $)
    supxrre $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                  sup ( A , RR* , < ) = sup ( A , RR , < ) ) $=
      ( vz cr wss cv cle wbr wral cxr clt csup wcel wa ressxr syl syl2anc cmnf
      wb wne wrex w3a wceq simp1 syl6ss supxrcl suprcl sseldi xrletri3 suprleub
      c0 leid mpdan supxrleub bitr4d mpbid xrleid simp2 n0 sylib sselda supxrub
      wex mnflt sylan mnfxr a1i adantr xrltletr syl3anc mp2and exlimdv mpd xrre
      wi ex syl22anc mpbir2and ) CEFZCULUAZBGAGZHIBCJAEUBZUCZCKLMZCELMZUDZWEWFH
      IZWFWEHIZWDWEKNZWFKNZWGWHWIOTWDCKFZWJWDCEKVTWAWCUEZPUFZCUGQZWDEKWFPABCUHZ
      UIZWEWFUJRWDWFWFHIZWHWDWFENZWRWPWFUMQWDWRDGZWFHIDCJZWHWDWSWRXATWPABDCWFUK
      UNWDWLWKWHXATWNWQDCWFUORUPUQZWDWEWEHIZWIWDWJXCWOWEURQWDXCWBWEHIACJZWIWDWL
      WJXCXDTWNWOACWEUORWDWEENZWIXDTWDWJWSSWELIZWHXEWOWPWDWTCNZDVDZXFWDWAXHVTWA
      WCUSDCUTVAWDXGXFDWDXGXFWDXGOZSWTLIZWTWEHIZXFXIWTENXJWDCEWTWMVBZWTVEQWDWLX
      GXKWNCWTVCVFXISKNZWTKNWJXJXKOXFVPXMXIVGVHXIEKWTPXLUIWDWJXGWOVISWTWEVJVKVL
      VQVMVNXBWEWFVOVRABACWEUKUNUPUQVS $.
      $( [7-Sep-2014] $) $( [18-Oct-2005] $)

    $( The supremum of a bounded-above nonempty set of reals is real. $)
    supxrbnd $p |- ( ( A C_ RR /\ A =/= (/) /\ sup ( A , RR* , < ) < +oo ) ->
                    sup ( A , RR* , < ) e. RR ) $=
      ( vy vx cr wss c0 wne cxr clt csup cpnf wbr cv cle wral wrex wa wn syl6bb
      wcel ressxr sstr mpan2 wi supxrcl pnfxr xrltne mp3an2 necomd ex supxrunb2
      syl wceq wb adantlr rexr ad2antlr xrlenlt con2bid syl2anc rexbidva rexnal
      ssel2 ralbidva bitr3d ralnex necon2abid sylibrd imp sylan 3adant2 supxrre
      w3a suprcl eqeltrd syld3an3 ) ADEZAFGZAHIJZKILZBMZCMZNLZBAOZCDPZVSDTVQVTW
      EVRVQAHEZVTWEVQDHEWFUAADHUBUCWFVTWEWFVTVSKGZWEWFVSHTZVTWGUDAUEWHVTWGWHVTQ
      KVSWHKHTVTKVSGUFVSKUGUHUIUJULWFWEVSKWFVSKUMZWDRZCDOZWERWFWBWAILZBAPZCDOWI
      WKCBAUKWFWMWJCDWFWBDTZQZWMWCRZBAPWJWOWLWPBAWOWAATZQWAHTZWBHTZWLWPUNWFWQWR
      WNAHWAVCUOWNWSWFWQWBUPUQWRWSQWCWLWAWBURUSUTVAWCBAVBSVDVEWDCDVFSVGVHVIVJVK
      VQVRWEVMVSADIJDCBAVLCBAVNVOVP $.
      $( [22-May-2007] $) $( [19-Jan-2006] $)

    $( The supremum of a nonempty set of reals is greater than minus
       infinity. $)
    supxrgtmnf $p |- ( ( A C_ RR /\ A =/= (/) ) ->
                     -oo < sup ( A , RR* , < ) ) $=
      ( cr wss c0 wne wa cxr clt csup wcel cpnf wceq wo cmnf wn supxrbnd 3expia
      wbr con3d syl wb ressxr sstr mpan2 supxrcl adantr nltpnft sylibrd mnfltxr
      orrd ) ABCZADEZFZAGHIZBJZUNKLZMNUNHRUMUOUPUMUOOUNKHRZOZUPUMUQUOUKULUQUOAP
      QSUMUNGJZUPURUAUKUSULUKAGCZUSUKBGCUTUBABGUCUDAUETUFUNUGTUHUJUNUIT $.
      $( [25-May-2007] $) $( [2-Feb-2006] $)

    $( The supremum of a nonempty set of reals is real iff it is less than plus
       infinity. $)
    supxrre1 $p |- ( ( A C_ RR /\ A =/= (/) ) ->
            ( sup ( A , RR* , < ) e. RR <-> sup ( A , RR* , < ) < +oo ) ) $=
      ( cr wss c0 wne wa cxr clt csup wcel cmnf wbr cpnf wb ressxr sstr supxrcl
      mpan2 xrrebnd 3syl adantr supxrgtmnf biantrurd bitr4d ) ABCZADEZFZAGHIZBJ
      ZKUHHLZUHMHLZFZUKUEUIULNZUFUEAGCZUHGJUMUEBGCUNOABGPRAQUHSTUAUGUJUKAUBUCUD
      $.
      $( [26-May-2007] $) $( [5-Feb-2006] $)

    $( The supremum of a nonempty set of reals is real iff it is not plus
       infinity. $)
    supxrre2 $p |- ( ( A C_ RR /\ A =/= (/) ) ->
            ( sup ( A , RR* , < ) e. RR <-> sup ( A , RR* , < ) =/= +oo ) ) $=
      ( cr wss c0 wne wa cxr clt csup wcel cpnf supxrre1 wb wceq wn ressxr sstr
      wbr mpan2 supxrcl nltpnft 3syl necon2abid adantr bitrd ) ABCZADEZFAGHIZBJ
      UHKHRZUHKEZALUFUIUJMUGUFUIUHKUFAGCZUHGJUHKNUIOMUFBGCUKPABGQSATUHUAUBUCUDU
      E $.
      $( [8-Dec-2007] $) $( [5-Feb-2006] $)

    $( A member of a set of extended reals is greater than or equal to the
       set's infimum.  (Contributed by Mario Carneiro, 16-Mar-2014.) $)
    infmxrlb $p |- ( ( A C_ RR* /\ B e. A ) -> sup ( A , RR* , `' < ) <_ B ) $=
      ( vx vy vz cxr wss wcel wa clt ccnv csup wbr wn cv wral wrex wi wor wb
      cle xrinfmss2 xrltso cnvso mpbi supub syl imp infmxrcl brcnvg sylan mtbid
      adantr ssel2 xrlenlt syl2anc mpbird ) AFGZBAHZIZAFJKZLZBUAMZBVBJMZNZUTVBB
      VAMZVDURUSVFNZURCOZDOZVAMNDAPVIVHVAMVIEOVAMEAQRDFPICFQUSVGRCDEAUBCDEFABVA
      FJSFVASUCFJUDUEUFUGUHURVBFHZUSVFVDTAUIZVBBFAJUJUKULUTVJBFHVCVETURVJUSVKUM
      AFBUNVBBUOUPUQ $.
      $( [16-Mar-2014] $)

    $( The infimum of a set of extended reals is greater than or equal to a
       lower bound.  (Revised by Mario Carneiro, 6-Sep-2014.) $)
    infmxrgelb $p |- ( ( A C_ RR* /\ B e. RR* ) ->
                     ( B <_ sup ( A , RR* , `' < ) <-> A. x e. A B <_ x ) ) $=
      ( vy vz cxr wcel wa clt wbr wn cv wral cle wrex wb wor cvv brcnvg sylancl
      wss ccnv csup wi xrinfmss2 adantr simpl simpr xrltso mpbi suplub2 syl3anc
      cnvso supex vex rexbidv 3bitr3d notbid ralnex syl6bbr id infmxrcl xrlenlt
      syl2anr simplr sselda syl2anc ralbidva 3bitr4d ) BFUAZCFGZHZBFIUBZUCZCIJZ
      KZALZCIJZKZABMZCVNNJZCVQNJZABMVLVPVRABOZKVTVLVOWCVLCVNVMJZCVQVMJZABOZVOWC
      VLDLZELZVMJKEBMWHWGVMJWHVQVMJABOUDEFMHDFOZVJVKWDWFPVJWIVKDEABUEUFVJVKUGZV
      JVKUHZDEAFBCVMFIQFVMQUIFIUMUJZUKULVLVKVNRGWDVOPWKFBVMWLUNCVNFRISTVLWEVRAB
      VLVKVQRGWEVRPWKAUOCVQFRISTUPUQURVRABUSUTVKVKVNFGWAVPPVJVKVABVBCVNVCVDVLWB
      VSABVLVQBGZHVKVQFGWBVSPVJVKWMVEVLBFVQWJVFCVQVCVGVHVI $.
      $( [6-Sep-2014] $) $( [16-Mar-2014] $)

    $( The real and extended real infima match when the real infimum exists.
       (Contributed by Mario Carneiro, 7-Sep-2014.) $)
    infmxrre $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                  sup ( A , RR* , `' < ) = sup ( A , RR , `' < ) ) $=
      ( vz cr wss cv cle wbr wral cxr clt csup wcel wa ressxr syl syl2anc cmnf
      wb c0 wne wrex w3a ccnv wceq simp1 syl6ss infmxrcl sseldi xrletri3 xrleid
      infmrcl infmxrgelb wex simp2 n0 sylib adantr sselda mnflt infmrgelb mpdan
      bitr4d mpbid wi mnfxr a1i xrltletr syl3anc mp2and infmxrlb sylan syl22anc
      leid xrre ex exlimdv mpd mpbir2and ) CEFZCUAUBZAGZBGHIBCJAEUCZUDZCKLUEZMZ
      CEWFMZUFZWGWHHIZWHWGHIZWEWGKNZWHKNZWIWJWKOTWECKFZWLWECEKWAWBWDUGZPUHZCUIQ
      ZWEEKWHPABCUMZUJZWGWHUKRWEWGWGHIZWJWEWLWTWQWGULQWEWTWGWCHIACJZWJWEWNWLWTX
      ATWPWQACWGUNRWEWGENZWJXATWEDGZCNZDUOZXBWEWBXEWAWBWDUPDCUQURWEXDXBDWEXDXBW
      EXDOWLXCENSWGLIZWGXCHIZXBWEWLXDWQUSWECEXCWOUTWEXFXDWESWHLIZWKXFWEWHENZXHW
      RWHVAQWEWHWHHIZWKWEXIXJWRWHVOQWEXJWHWCHIACJZWKWEXIXJXKTWRABACWHVBVCWEWNWM
      WKXKTWPWSACWHUNRVDVEZWESKNZWMWLXHWKOXFVFXMWEVGVHWSWQSWHWGVIVJVKUSWEWNXDXG
      WPCXCVLVMWGXCVPVNVQVRVSABACWGVBVCVDVEXLVT $.
      $( [7-Sep-2014] $)

  $}

