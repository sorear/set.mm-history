$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The reals are uncountable
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n x y z $.  $d k n x A $.  $d k n x B $.  $d k m y z F $.  $d k ph $.
    $d k n x M $.  $d n N $.
    rpnnen2.1 $e |- F = ( x e. ~P NN |->
      ( n e. NN |-> if ( n e. x , ( ( 1 / 3 ) ^ n ) , 0 ) ) ) $.
    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem1 $p |- ( ( A C_ NN /\ N e. NN ) ->
        ( ( F ` A ) ` N ) = if ( N e. A , ( ( 1 / 3 ) ^ N ) , 0 ) ) $=
      ( cn wss wcel cfv cv c1 co cexp cc0 cif cmpt wceq nnex fvmpt c3 cpw elpw2
      cdiv eleq2 ifbid mpteq2dv mptex sylbir fveq1d eleq1 oveq2 eqidd ifbieq12d
      eqid ovex cr 0re elexi ifex sylan9eq ) BGHZEGIEBDJZJECGCKZBIZLUAUDMZVDNMZ
      OPZQZJEBIZVFENMZOPZVBEVCVIVBBGUBZIVCVIRBGSUCABCGVDAKZIZVGOPZQVIVMDVNBRZCG
      VPVHVQVOVEVGOVNBVDUEUFUGFCGVHSUHTUIUJCEVHVLGVIVDERZVEVJVGOVKOVDEBUKVDEVFN
      ULVROUMUNVIUOVJVKOVFENUPOUQURUSUTTVA $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem2 $p |- ( A C_ NN -> ( F ` A ) : NN --> RR ) $=
      ( cn wss cr wf cv wcel c1 c3 co cc0 cif cmpt wceq nnex cvv cfv cdiv mp2an
      cexp cn0 1re 3nn nndivre nnnn0 reexpcl sylancr 0re ifcl adantl eqid fmptd
      sylancl elpw2 eleq2 ifbid mpteq2dv mptexg ax-mp fvmpt sylbir feq1d mpbird
      cpw ) BFGZFHBDUAZIFHCFCJZBKZLMUBNZVKUDNZOPZQZIVICFVOHVPVKFKZVOHKZVIVQVNHK
      ZOHKVRVQVMHKZVKUEKVSLHKMFKVTUFUGLMUHUCVKUIVMVKUJUKULVLVNOHUMUQUNVPUOUPVIF
      HVJVPVIBFVHZKVJVPRBFSURABCFVKAJZKZVNOPZQVPWADWBBRZCFWDVOWEWCVLVNOWBBVKUSU
      TVAEFTKVPTKSCFVOTVBVCVDVEVFVG $.
      $( [23-Aug-2014] $) $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem3 $p |- seq 1 ( + , ( F ` NN ) ) ~~> ( 1 / 2 ) $=
      ( cn cfv c1 c3 cdiv co cmin c2 wbr wtru cc wcel mp2an clt cc0 wceq vk cli
      caddc cseq cexp cr 1re 3nn nndivre recni a1i cabs cle 0re 3re 3pos ltleii
      recgt0ii absid 1lt3 wb recgt1 mpbi eqbrtri cn0 1nn0 cv cuz cif ssid simpr
      wss nnuz syl6eleqr rpnnen2lem1 sylancr iftrue syl eqtrd geolim2 trud exp1
      wa ax-mp wne 3cn ax-1cn ltneii pm3.2i divsubdir eqcomi dividi oveq1i df-3
      mp3an 2cn pncan eqtri 3eqtr3i oveq12i 2ne0 divcan7 breqtri ) UCECFZGUDZGH
      IJZGUEJZGXFKJZIJZGLIJZUBXEXIUBMNXFUAXDGXFOPZNXFGUFPHEPXFUFPZUGUHGHUIQZUJZ
      UKXFULFZGRMNXOXFGRXLSXFUMMXOXFTXMSXFUNXMHUOUPURUQXFUSQGHRMZXFGRMZUTHUFPSH
      RMXPXQVAUOUPHVBQVCVDUKGVEPNVFUKNUAVGZGVHFZPZWCZXRXDFZXREPZXFXRUEJZSVIZYDY
      AEEVLYCYBYETEVJYAXRXSENXTVKVMVNZAEBCXRDVOVPYAYCYEYDTYFYCYDSVQVRVSVTWAXIXF
      LHIJZIJZXJXGXFXHYGIXKXGXFTXNXFWBWDHHIJZXFKJZHGKJZHIJZXHYGYLYJHOPZGOPZYMHS
      WEZWCZYLYJTWFWGYMYOWFSHUNUOUPWHZWIZHGHWJWOWKYIGXFKHWFYQWLWMYKLHIYKLGUCJZG
      KJZLHYSGKWNWMLOPZYNYTLTWPWGLGWQQWRWMWSWTYNUUALSWEZWCYPYHXJTWGUUAUUBWPXAWI
      YRGLHXBWOWRXC $.
      $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem4 $p |- ( ( A C_ B /\ B C_ NN /\ k e. NN ) ->
      ( 0 <_ ( ( F ` A ) ` k ) /\ ( ( F ` A ) ` k ) <_ ( ( F ` B ) ` k ) ) ) $=
      ( wss cn wcel cc0 cfv cle wbr c1 c3 co cif 0re cr w3a cdiv cexp cn0 nnnn0
      cv 1re 3nn nndivre mp2an 3re 3pos recgt0ii ltleii expge0 sylancl 3ad2ant3
      mp3an1 leidi breq2 ifboth wceq wa sstr rpnnen2lem1 sylan breqtrrd reexpcl
      3impa wi sylancr a1i simp1 sseld ifle syl31anc 3adant1 3brtr4d jca ) BCHZ
      CIHZDUFZIJZUAZKWBBFLLZMNWEWBCFLLZMNWDKWBBJZOPUBQZWBUCQZKRZWEMWDKWIMNZKKMN
      ZKWJMNZWCVTWKWAWCWBUDJZKWHMNZWKWBUEZKWHSOTJPIJWHTJZUGUHOPUIUJZPUKULUMUNWQ
      WNWOWKWRWHWBUOURUPUQZKSUSWGWKWLWMWIKWIWJKMUTKWJKMUTVAUPVTWAWCWEWJVBZVTWAV
      CBIHWCWTBCIVDABEFWBGVEVFVIZVGWDWJWBCJZWIKRZWEWFMWDWITJZKTJZWKWGXBVJWJXCMN
      WCVTXDWAWCWQWNXDWRWPWHWBVHVKUQXEWDSVLWSWDBCWBVTWAWCVMVNWGXBWIKVOVPXAWAWCW
      FXCVBVTACEFWBGVEVQVRVS $.
      $( [31-Aug-2014] $) $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem5 $p |- ( ( A C_ NN /\ M e. NN ) ->
                        seq M ( + , ( F ` A ) ) e. dom ~~> ) $=
      ( vk cn wss wcel wa caddc cfv c1 cseq cli nnuz a1i cr wbr cdm 1nn wf ssid
      cv rpnnen2lem2 ax-mp ffvelrn sylan c2 co rpnnen2lem3 seqex breldm cuz cc0
      cdiv eleq2i rpnnen2lem4 mp3an2 sylan2br simpld simprd cvgcmp adantr simpr
      cle adantlr recnd iserex mpbid ) BHIZEHJZKZLBDMZNOPUAZJZLVOEOVPJVLVQVMVLG
      HDMZVONNHQNHJVLUBRVLHSVRUCZGUEZHJZVTVRMZSJVSVLHHIZVSHUDZAHCDFUFUGRHSVTVRU
      HUIVLHSVOUCWAVTVOMZSJZABCDFUFHSVTVOUHUIZLVRNOZVPJZVLWHNUJUQUKZPTWIACDFULW
      HWJPLVRNUMUNUGRVLVTNUOMZJZKZUPWEVGTZWEWBVGTZWLVLWAWNWOKZHWKVTQURVLWCWAWPW
      DABHGCDFUSUTVAZVBWMWNWOWQVCVDVEVNGVONEHQVLVMVFVNWAKWEVLWAWFVMWGVHVIVJVK
      $.
      $( [30-Apr-2014] $) $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem6 $p |- ( ( A C_ NN /\ M e. NN ) ->
        sum_ k e. ( ZZ>= ` M ) ( ( F ` A ) ` k ) e. RR ) $=
      ( cn wss wcel wa cv cfv cuz eqid cz nnz adantl eqidd cr wf rpnnen2lem2 c1
      ad2antrr nnuz uztrn2 adantll ffvelrn syl2anc rpnnen2lem5 isumrecl ) BHIZF
      HJZKZCLZBEMZMZCUPFFNMZUROUMFPJULFQRUNUOURJZKZUQSUTHTUPUAZUOHJZUQTJULVAUMU
      SABDEGUBUDUMUSVBULUCUOFHUEUFUGHTUOUPUHUIABDEFGUJUK $.
      $( [30-Apr-2014] $) $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem7 $p |- ( ( A C_ B /\ B C_ NN /\ M e. NN ) ->
        sum_ k e. ( ZZ>= ` M ) ( ( F ` A ) ` k ) <_
        sum_ k e. ( ZZ>= ` M ) ( ( F ` B ) ` k ) ) $=
      ( wss cn wcel w3a cfv syl eqidd cr sylan wf rpnnen2lem2 syldan cv eqid cz
      cuz simp3 nnz wa c1 nnuz uztrn2 sstr 3adant3 ffvelrn 3ad2ant2 cle wbr cc0
      rpnnen2lem4 3expa 3adantl3 caddc cseq cli cdm rpnnen2lem5 syl2anc 3adant1
      simprd isumle ) BCIZCJIZGJKZLZDUAZBFMZMZVNCFMZMZDVOVQGGUDMZVSUBVMVLGUCKVJ
      VKVLUEZGUFNVMVNVSKZUGZVPOVMWAVNJKZVPPKZVMVLWAWCVTUHVNGJUIUJQZVMJPVORZWCWD
      VMBJIZWFVJVKWGVLBCJUKULZABEFHSNJPVNVOUMQTWBVROVMWAWCVRPKZWEVMJPVQRZWCWIVK
      VJWJVLACEFHSUNJPVNVQUMQTVMWAWCVPVRUOUPZWEVJVKWCWKVLVJVKWCWKVJVKWCLUQVPUOU
      PWKABCDEFHURVHUSUTTVMWGVLVAVOGVBVCVDZKWHVTABEFGHVEVFVKVLVAVQGVBWLKVJACEFG
      HVEVGVI $.
      $( [30-Apr-2014] $) $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem8 $p |- ( ( A C_ NN /\ M e. NN ) ->
       sum_ k e. NN ( ( F ` A ) ` k ) =
         ( sum_ k e. ( 1 ... ( M - 1 ) ) ( ( F ` A ) ` k ) +
           sum_ k e. ( ZZ>= ` M ) ( ( F ` A ) ` k ) ) ) $=
      ( cn wss wcel wa cv cfv c1 cuz nnuz eqid simpr cr adantr eqidd wf ffvelrn
      rpnnen2lem2 sylan recnd caddc cseq cli cdm rpnnen2lem5 mpan2 isumsplit
      1nn ) BHIZFHJZKZCLZBEMZMZCUSNFFOMZHPVAQUOUPRUQURHJZKZUTUAVCUTUQHSUSUBZVBU
      TSJUOVDUPABDEGUDTHSURUSUCUEUFUOUGUSNUHUIUJJZUPUONHJVEUNABDENGUKULTUM $.
      $( [30-Apr-2014] $) $( [13-May-2013] $)

    $( Lemma for ~ rpnnen2 . $)
    rpnnen2lem9 $p |- ( M e. NN ->
       sum_ k e. ( ZZ>= ` M ) ( ( F ` ( NN \ { M } ) ) ` k ) =
         ( 0 + ( ( ( 1 / 3 ) ^ ( M + 1 ) ) / ( 1 - ( 1 / 3 ) ) ) ) ) $=
      ( cn wcel cfv c1 caddc co cc0 c3 cdiv cr syl wceq clt wbr cuz cv csn cdif
      csu cexp cmin eqid nnz wa eqidd cc nnuz uztrn2 wf difss rpnnen2lem2 ax-mp
      wss ffvelrni recnd cseq cli cdm rpnnen2lem5 isum1p cif rpnnen2lem1 eldifn
      mpan wn snidg nsyl3 iffalse eqtrd cvv peano2nn sylan ovex a1i 1re nndivre
      cz 3nn mp2an recni cabs cle 0re 3pos recgt0ii ltleii absid 1lt3 wb recgt1
      3re mpbi eqbrtri cn0 wne nnre adantr eluzle adantl nnltp1le syldan mpbird
      nnnn0 ltne syl3anc eldifsn sylanbrc iftrue geolim2 isumclim oveq12d ) EGH
      ZEUAIZBUBZGEUCZUDZDIZIZBUEEYCIZEJKLZUAIZYDBUEZKLMJNOLZYFUFLZJYIUGLZOLZKLX
      RYDBYCEXSXSUHEUIXRXTXSHUJZYDUKYMXTGHZYDULHZJXTEGUMUNYNYDGPXTYCYBGUSZGPYCU
      OGYAUPZAYBCDFUQURUTVAZQYPXRKYCEVBVCVDHYQAYBCDEFVEVJVFXRYEMYHYLKXRYEEYBHZY
      IEUFLZMVGZMYPXRYEUUARYQAYBCDEFVHVJXRYSVKUUAMRYSEYAHXREGYAVIEGVLVMYSYTMVNQ
      VOXRYDYLBYCYFVPYGYGUHXRYFGHZYFWCHEVQZYFUIQXRXTYGHZUJZYDUKUUEYNYOXRUUBUUDY
      NUUCJXTYFGUMUNVRZYRQYLVPHXRYJYKOVSVTXRYIBYCYFYIULHXRYIJPHNGHYIPHZWAWDJNWB
      WEZWFVTYIWGIZJSTXRUUIYIJSUUGMYIWHTUUIYIRUUHMYIWIUUHNWQWJWKWLYIWMWEJNSTZYI
      JSTZWNNPHMNSTUUJUUKWOWQWJNWPWEWRWSVTXRUUBYFWTHUUCYFXIQUUEYDXTYBHZYIXTUFLZ
      MVGZUUMUUEYNYDUUNRZUUFYPYNUUOYQAYBCDXTFVHVJQUUEUULUUNUUMRUUEYNXTEXAZUULUU
      FUUEEPHZXTPHZEXTSTZUUPXRUUQUUDEXBXCUUEYNUURUUFXTXBQUUEUUSYFXTWHTZUUDUUTXR
      YFXTXDXEXRUUDYNUUSUUTWOUUFEXTXFXGXHEXTXJXKXTGEXLXMUULUUMMXNQVOXOXPXQVO $.
      $( [30-Apr-2014] $) $( [13-May-2013] $)

    ${
      rpnnen2.2 $e |- ( ph -> A C_ NN ) $.
      rpnnen2.3 $e |- ( ph -> B C_ NN ) $.
      rpnnen2.4 $e |- ( ph -> m e. ( A \ B ) ) $.
      rpnnen2.5 $e |- ( ph ->
                        A. n e. NN ( n < m -> ( n e. A <-> n e. B ) ) ) $.
      rpnnen2.6 $e |- ( ps <-> sum_ k e. NN ( ( F ` A ) ` k ) =
                               sum_ k e. NN ( ( F ` B ) ` k ) ) $.
      $( Lemma for ~ rpnnen2 . $)
      rpnnen2lem10 $p |- ( ( ph /\ ps ) ->
            sum_ k e. ( ZZ>= ` m ) ( ( F ` A ) ` k ) =
            sum_ k e. ( ZZ>= ` m ) ( ( F ` B ) ` k ) ) $=
        ( c1 co wceq cn wcel wa cmin cfz cfv csu cuz caddc simpr sylib wss cdif
        cv eldifi ssel2 sylan2 syl2anc rpnnen2lem8 c3 cdiv cexp cc0 cif clt wbr
        wb cz cle w3a 1z nnz elfzm11 sylancr biimpa sylan simp3d wi wral elfznn
        breq1 eleq1 bibi12d imbi12d rcla4cva ifbid rpnnen2lem1 3eqtr4d sumeq2dv
        syl2an mpd oveq1d eqtrd adantr 3eqtr3d cr rpnnen2lem6 fzfid rpnnen2lem2
        wf syl ffvelrn fsumrecl readdcan syl3anc mpbid ) ABUAZPGULZPUBQZUCQZFUL
        ZEIUDZUDZFUEZXFUFUDZXIDIUDUDZFUEZUGQZXLXMXKFUEZUGQZRZXOXQRZXESXNFUEZSXK
        FUEZXPXRXEBYAYBRABUHOUIAYAXPRBAYAXHXNFUEZXOUGQZXPADSUJZXFSTZYAYDRKAYEXF
        DEUKTZYFKMYGYEXFDTYFXFDEUMDSXFUNUOUPZCDFHIXFJUQUPAYCXLXOUGAXHXNXKFAXIXH
        TZUAZXIDTZPURUSQXIUTQZVAVBZXIETZYLVAVBZXNXKYJYKYNYLVAYJXIXFVCVDZYKYNVEZ
        YJXIVFTZPXIVGVDZYPAYFYIYRYSYPVHZYHYFYIYTYFPVFTXFVFTYIYTVEVIXFVJXIPXFVKV
        LVMVNVOAHULZXFVCVDZUUADTZUUAETZVEZVPZHSVQXISTZYPYQVPZYINXIXGVRZUUFUUHHX
        ISUUAXIRZUUBYPUUEYQUUAXIXFVCVSUUJUUCYKUUDYNUUAXIDVTUUAXIEVTWAWBWCWHWIWD
        AYEUUGXNYMRYIKUUICDHIXIJWEWHAESUJZUUGXKYORYILUUICEHIXIJWEWHWFWGWJWKWLAY
        BXRRZBAUUKYFUULLYHCEFHIXFJUQUPWLWMAXSXTVEZBAXOWNTZXQWNTZXLWNTUUMAYEYFUU
        NKYHCDFHIXFJWOUPAUUKYFUUOLYHCEFHIXFJWOUPAXHXKFAPXGWPASWNXJWRZUUGXKWNTYI
        AUUKUUPLCEHIJWQWSUUISWNXIXJWTWHXAXOXQXLXBXCWLXD $.
        $( [30-Apr-2014] $) $( [13-May-2013] $)

      $( Lemma for ~ rpnnen2 . $)
      rpnnen2lem11 $p |- ( ph -> -. ps ) $=
        ( wcel cn c1 c3 co cv cuz cfv csu wne wn clt wbr wss cdif eldifi sylan2
        ssel2 syl2anc rpnnen2lem6 cdiv cexp cn0 3nn nnrecre ax-mp nnnn0 reexpcl
        cr syl sylancr c2 crp cz nnrp rpreccl mp2b nnz rpexpcl rehalfcl csn cle
        rpre snssd ssdif sstrd wb ssconb mpbird difss rpnnen2lem7 syl3anc caddc
        a1i cc0 cmin wceq rpnnen2lem9 cmul cc recni expp1 recnd 3cn 3ne0 divrec
        mp3an23 eqtr4d oveq1d wa ax-1cn pm3.2i divsubdir mp3an oveq1i 2cn pncan
        df-3 mp2an eqtri dividi 3eqtr3ri oveq2i 2ne0 syl5eq eqtrd oveq2d addid2
        divcan7 3eqtrd breqtrd rphalflt lelttrd uztrn2 sylan rpnnen2lem1 syldan
        cif nnuz sumeq2dv wral cfn wo uzid 3syl ralsn oveq2 eleq1d sbcie sylibr
        wsbc vex bitri ssid orcd sumss2 syl21anc sumsn 3eqtr2d eqbrtrrd ltletrd
        snssi ltne rpnnen2lem10 ex necon3ad mpd ) AGUAZUBUCZFUAZDIUCUCFUDZUVDUV
        EEIUCUCFUDZUEZBUFAUVGVDPZUVFVDPZUVGUVFUGUHUVHAEQUIZUVCQPZUVILADQUIZUVCD
        EUJZPZUVLKMUVOUVMUVCDPZUVLUVCDEUKZDQUVCUMULUNZCEFHIUVCJUOUNZAUVMUVLUVJK
        UVRCDFHIUVCJUOUNZAUVGRSUPTZUVCUQTZUVFUVSAUWAVDPZUVCURPZUWBVDPZSQPZUWCUS
        SUTVAZAUVLUWDUVRUVCVBVEZUWAUVCVCVFUVTAUVGUWBVGUPTZUWBUVSAUWEUWIVDPAUWBV
        HPZUWEAUWAVHPZUVCVIPZUWJUWFSVHPUWKUSSVJSVKVLAUVLUWLUVRUVCVMZVEUWAUVCVNV
        FZUWBVRVEZUWBVOVEZUWOAUVGUVDUVEQUVCVPZUJZIUCUCFUDZUWIVQAEUWRUIZUWRQUIZU
        VLUVGUWSVQUHAUWTUWQQEUJZUIZAUWQUVNUXBAUVCUVNMVSAUVMUVNUXBUIKDQEVTVEWAAU
        VKUWQQUIZUWTUXCWBLAUVCQUVRVSZEUWQQWCUNWDUXAAQUWQWEWIUVRCEUWRFHIUVCJWFWG
        AUWSWJUWAUVCRWHTUQTZRUWAWKTZUPTZWHTZWJUWIWHTZUWIAUVLUWSUXIWLUVRCFHIUVCJ
        WMVEAUXHUWIWJWHAUXHUWBSUPTZUXGUPTZUWIAUXFUXKUXGUPAUXFUWBUWAWNTZUXKAUWAW
        OPUWDUXFUXMWLUWAUWGWPUWHUWAUVCWQVFAUWBWOPZUXKUXMWLZAUWBUWOWRZUXNSWOPZSW
        JUEZUXOWSWTUWBSXAXBVEXCXDAUXLUXKVGSUPTZUPTZUWIUXGUXSUXKUPSRWKTZSUPTZSSU
        PTZUWAWKTZUXSUXGUXQRWOPZUXQUXRXEZUYBUYDWLWSXFUXQUXRWSWTXGZSRSXHXIUYAVGS
        UPUYAVGRWHTZRWKTZVGSUYHRWKXMXJVGWOPZUYEUYIVGWLXKXFVGRXLXNXOXJUYCRUWAWKS
        WSWTXPXJXQXRAUXNUXTUWIWLZUXPUXNUYJVGWJUEZXEUYFUYKUYJUYLXKXSXGUYGUWBVGSY
        DXBVEXTYAYBAUWIWOPUXJUWIWLAUWIUWPWRUWIYCVEYEYFAUWJUWIUWBUGUHUWNUWBYGVEY
        HAUVDUVEUWQIUCUCZFUDZUWBUVFVQAUYNUVDUVEUWQPUWAUVEUQTZWJYMZFUDZUWQUYOFUD
        ZUWBAUVDUYMUYPFAUVEUVDPZUVEQPZUYMUYPWLZAUVLUYSUYTUVRRUVEUVCQYNYIYJAUXDU
        YTVUAUXECUWQHIUVEJYKYJYLYOAUWQUVDUIUYOWOPZFUWQYPZUVDUVDUIZUVDYQPZYRUYRU
        YQWLAUVCUVDAUVLUWLUVCUVDPUVRUWMUVCYSYTVSAUXNVUCUXPVUCVUBFUVCUUFUXNVUBFU
        VCGUUGZUUAVUBUXNFUVCVUFUVEUVCWLUYOUWBWOUVEUVCUWAUQUUBZUUCUUDUUHUUEAVUDV
        UEVUDAUVDUUIWIUUJUWQUVDUYOFUVCUUKUULAUVLUXNUYRUWBWLUVRUXPUYOUWBFUVCQVUG
        UUMUNUUNAUWQDUIZUVMUVLUYNUVFVQUHAUVOUVPVUHMUVQUVCDUUQYTKUVRCUWQDFHIUVCJ
        WFWGUUOUUPUVGUVFUURWGABUVFUVGABUVFUVGWLABCDEFGHIJKLMNOUUSUUTUVAUVB $.
        $( [13-May-2013] $)
    $}

    $( The other half of ~ rpnnen , where we show an injection from sets of
       natural numbers to real numbers.  The obvious choice for this is binary
       expansion, but it has the unfortunate property that it does not produce
       an injection on numbers which end with all 0's or all 1's (the more
       well-known decimal version of this is ~ 0.999... ).  Instead, we opt for
       a ternary expansion, which produces (a scaled version of) the Cantor
       set.  Since the Cantor set is riddled with gaps, we can show that any
       two sequences that are not equal must differ somewhere, and when they
       do, they are placed a finite distance apart, thus ensuring that the map
       is injective.

       Our map assigns to each subset ` A ` of the natural numbers the number
       ` sum_ k e. A ( 3 ^ -u k ) = sum_ k e. NN ( ( F `` A ) `` k ) ` , where
       ` ( ( F `` A ) `` k ) = if ( k e. A , ( 3 ^ -u k ) , 0 ) ) `
       ( ~ rpnnen2lem1 ).  This is an infinite sum of real numbers
       ( ~ rpnnen2lem2 ), and since ` A C_ B ` implies
       ` ( F `` A ) <_ ( F `` B ) ` ( ~ rpnnen2lem4 ) and ` ( F `` NN ) `
       converges to ` 1 / 2 ` ( ~ rpnnen2lem3 ) by ~ geoisum1 , the sum is
       convergent to some real ( ~ rpnnen2lem5 and ~ rpnnen2lem6 ) by the
       comparison test for convergence ~ cvgcmp .  The comparison test also
       tells us that ` A C_ B ` implies ` sum_ ( F `` A ) <_ sum_ ( F `` B ) `
       ( ~ rpnnen2lem7 ).

       Putting it all together, if we have two sets ` x =/= y ` , there must
       differ somewhere, and so there must be an ` m ` such that
       ` A. n < m ( n e. x <-> n e. y ) ` but ` m e. ( x \ y ) ` or vice
       versa.  In this case, we split off the first ` m - 1 ` terms
       ( ~ rpnnen2lem8 ) and cancel them ( ~ rpnnen2lem10 ), since these are
       the same for both sets.  For the remaining terms, we use the subset
       property to establish that
       ` sum_ ( F `` y ) <_ sum_ ( F `` ( NN \ { m } ) ) ` and
       ` sum_ ( F `` { m } ) <_ sum_ ( F `` x ) ` (where these sums are only
       over ` ( ZZ>= `` m ) ` ), and since
       ` sum_ ( F `` ( NN \ { m } ) ) = ( 3 ^ -u m ) / 2 ` ( ~ rpnnen2lem9 )
       and ` sum_ ( F `` { m } ) = ( 3 ^ -u m ) ` , we establish that
       ` sum_ ( F `` y ) < sum_ ( F `` x ) ` ( ~ rpnnen2lem11 ) so that they
       must be different.  By contraposition, we find that this map is an
       injection.  (Contributed by Mario Carneiro, 13-May-2013.)  (The proof
       was shortened by Mario Carneiro, 30-Apr-2014.) $)
    rpnnen2 $p |- ~P NN ~<_ ( 0 [,] 1 ) $=
      ( vk vm cn wcel c1 wbr cv cfv cr cle wss a1i wa wceq wi wn vy cpw cvv cc0
      vz cicc co cdom nnex pwex csu elpwi cuz nnuz sumeq1i rpnnen2lem6 syl5eqel
      1nn mpan2 syl cz 1z eqidd wf rpnnen2lem2 ffvelrn sylan caddc cseq cli cdm
      rpnnen2lem5 sylancl ssid rpnnen2lem4 mp3an2 simpld isumge0 cdiv 1re ax-mp
      c2 rehalfcl rpnnen2lem7 mp3an23 eqid cc elnnuz ffvelrni recnd rpnnen2lem3
      sylbir adantl isumclim breqtrd halflt1 ltleii letrd 0re elicc2i syl3anbrc
      syl5eqbr wne clt wb wral cdif wrex wo cun ssdifss unss biimpi syl2an eqss
      c0 ssdif0 anbi12i un00 3bitri necon3bii nnwo ex sseld df-ral con34b eldif
      imbi2i bitri nnre syld simpll simplr simprl simprr rpnnen2lem11 rexlimdva
      imp wal expr orbi12i elun xor 3bitr4ri con1bii albii alral syl2anr imbi1d
      ltnle ralbidva syl5ibr syl5bi rexun syl6ib biid bicom ralbii sylibr eqcom
      reximdva jaod necon4ad fveq2 fveq1d sumeq2sdv impbid1 dom2 ) GUBZUCHUVIUD
      IUFUGZUHJGUIUJUAUEUVIUVJGEKZUAKZCLZLZEUKZGUVKUEKZCLZLZEUKZUCUVLUVIHZUVOMH
      ZUDUVONJUVOINJUVOUVJHUVTUVLGOZUWAUVLGULZUWBUVOIUMLZUVNEUKZMGUWDUVNEUNUOZU
      WBIGHZUWEMHURAUVLEBCIDUPUSUQUTZUVTUVNEUVMIGUNIVAHUVTVBPZUVTUVKGHZQUVNVCUV
      TGMUVMVDZUWJUVNMHUVTUWBUWKUWCAUVLBCDVEUTGMUVKUVMVFVGUVTUWBUWGVHUVMIVIVJVK
      HUWCURAUVLBCIDVLVMUVTUWBUWJUDUVNNJZUWCUWBUWJQUWLUVNUVKGCLZLZNJZUWBGGOZUWJ
      UWLUWOQGVNZAUVLGEBCDVOVPVQVGVRUVTUVOIWBVSUGZIUWHUWRMHZUVTIMHZUWSVTIWCWAZP
      ZUWTUVTVTPUVTUVOUWEUWRNUWFUVTUWEUWDUWNEUKZUWRNUVTUWBUWEUXCNJZUWCUWBUWPUWG
      UXDUWQURAUVLGEBCIDWDWEUTUVTUWNUWREUWMIMUWDUWDWFUWIUVTUVKUWDHZQUWNVCUXEUWN
      WGHZUVTUXEUWJUXFUVKWHUWJUWNGMUVKUWMUWPGMUWMVDUWQAGBCDVEWAWIWJWLWMUXBVHUWM
      IVIUWRVJJUVTABCDWKPWNWOXBUWRINJUVTUWRIUXAVTWPWQPWRUDIUVOWSVTWTXAUVTUVPUVI
      HZQZUVOUVSRZUVLUVPRZUXHUXIUVLUVPUXHUVLUVPXCZBKZFKZXDJZUXLUVLHZUXLUVPHZXEZ
      SZBGXFZFUVLUVPXGZXHZUXSFUVPUVLXGZXHZXIZUXITZUXHUXKUXSFUXTUYBXJZXHZUYDUXHU
      XKUXMUXLNJZBUYFXFZFUYFXHZUYGUXHUXKUYJUXHUYFGOZUYFXPXCZUYJUXKUVTUWBUVPGOZU
      YKUXGUWCUVPGULZUWBUXTGOZUYBGOZUYKUYMUVLGUVPXKUVPGUVLXKUYOUYPQUYKUXTUYBGXL
      XMXNXNZUXKUYLUVLUVPUYFXPUXJUVLUVPOZUVPUVLOZQUXTXPRZUYBXPRZQUYFXPRUVLUVPXO
      UYRUYTUYSVUAUVLUVPXQUVPUVLXQXRUXTUYBXSXTYAXMFBUYFYBXNYCUXHUYIUXSFUYFUXHUX
      MUYFHZQUXMGHZUYIUXSSUXHVUBVUCUXHUYFGUXMUYQYDYRUYIUYHTZUXQSZBYSZVUCUXSUYIU
      XLUYFHZUYHSZBYSVUFUYHBUYFYEVUHVUEBVUHVUDVUGTZSVUEVUGUYHYFVUIUXQVUDUXQVUGU
      XLUXTHZUXLUYBHZXIUXOUXPTQZUXPUXOTQZXIVUGUXQTVUJVULVUKVUMUXLUVLUVPYGUXLUVP
      UVLYGUUAUXLUXTUYBUUBUXOUXPUUCUUDUUEYHYIUUFYIVUFUXSVUCVUEBGXFVUEBGUUGVUCUX
      RVUEBGVUCUXLGHZQUXNVUDUXQVUNUXLMHUXMMHUXNVUDXEVUCUXLYJUXMYJUXLUXMUUJUUHUU
      IUUKUULUUMUTUVAYKUXSFUXTUYBUUNUUOUVTUWBUYMUYDUYESUXGUWCUYNUWBUYMQZUYAUYEU
      YCVUOUXSUYEFUXTVUOUXMUXTHZUXSUYEVUOVUPUXSQZQUXIAUVLUVPEFBCDUWBUYMVUQYLUWB
      UYMVUQYMVUOVUPUXSYNVUOVUPUXSYOUXIUUPYPYTYQVUOUXSUYEFUYBVUOUXMUYBHZUXSUYEV
      UOVURUXSQZQZUXIAUVPUVLEFBCDUWBUYMVUSYMUWBUYMVUSYLVUOVURUXSYNVUTUXSUXNUXPU
      XOXEZSZBGXFVUOVURUXSYOVVBUXRBGVVAUXQUXNUXPUXOUUQYHUURUUSUVOUVSUUTYPYTYQUV
      BXNYKUVCUXJGUVNUVREUXJUVKUVMUVQUVLUVPCUVDUVEUVFUVGUVHWA $.
      $( [30-Apr-2014] $) $( [13-May-2013] $)
  $}

  ${
    $d j k m n x y $.
    $( The cardinality of the continuum is the same as the powerset of
       ` om ` .  This is a stronger statement than ~ ruc , which only asserts
       that ` RR ` is uncountable, i.e. has a cardinality larger than ` om ` .
       The main proof is in two parts, ~ rpnnen1 and ~ rpnnen2 , each showing
       an injection in one direction, and this last part uses ~ sbth to prove
       that the sets are equinumerous.  By constructing explicit injections, we
       avoid the use of AC. (Contributed by Mario Carneiro, 13-May-2013.) $)
    rpnnen $p |- RR ~~ ~P NN $=
      ( vx vm vk vn vy cr cn cdom wbr cen cq cmap co cdiv clt cmpt c2o nnex cc0
      mp2an vj cpw cv crab csup wceq oveq1 breq1d cbvrabv oveq2 rabbidv supeq1d
      cz oveq12d cbvmptv breq2 oveq1d mpteq2dv syl5eq rpnnen1 csdm qnnen canth2
      ensdomtr sdomdom pwex mapdom1 mp2b pw2en enref ovex mapen domentr cxp com
      id 2onn elexi mapxpen xpnnen entri entr4i domtr cicc wcel cexp cif elequ2
      c1 c3 ifbid elequ1 eqidd ifbieq12d syl6eq rpnnen2 cvv wss 0re 1re iccssre
      ssdomg mp2 sbth ) FGUBZHIZXEFHIZFXEJIFKGLMZHIXHXEHIZXFABUCZCUCZNMZAUCZOIZ
      BUMUDZCDEFUAGXJUAUCZNMZEUCZOIZBUMUDZFOUEZXPNMZPZPXNDUCZXKNMZXMOIBDUMXJYDU
      FXLYEXMOXJYDXKNUGUHUIEAFYCCGXOFOUEZXKNMZPZXRXMUFZYCCGXLXROIZBUMUDZFOUEZXK
      NMZPYHUACGYBYMXPXKUFZYAYLXPXKNYNFXTYKOYNXSYJBUMYNXQXLXROXPXKXJNUJUHUKULYN
      VPUNUOYICGYMYGYIYLYFXKNYIFYKXOOYIYJXNBUMXRXMXLOUPUKULUQURUSUOUTXHQGLMZGLM
      ZHIZYPXEJIXIXHXEGLMZHIZYRYPJIZYQKXEVAIZKXEHIYSKGJIGXEVAIUUAVBGRVCKGXEVDTK
      XEVEKXEGGRVFRVGVHXEYOJIGGJIYTGRVIZGRVJXEYOGGQGLVKZRVLTXHYRYPVMTYPYOXEUUCY
      PQGGVNZLMZYOQGGQVOVQVRZRRVSQQJIUUDGJIUUEYOJIQUUFVJVTQQUUDGUUFRVLTWAUUBWBX
      HYPXEVMTFXHXEWCTXESWIWDMZHIUUGFHIZXGACEXEDGYDXRWEZWIWJNMZYDWFMZSWGZPZPEAX
      EUUMCGXKXMWEZUUJXKWFMZSWGZPZYIUUMDGYDXMWEZUUKSWGZPUUQYIDGUULUUSYIUUIUURUU
      KSEADWHWKURDCGUUSUUPYDXKUFZUURUUNUUKSUUOSDCAWLYDXKUUJWFUJUUTSWMWNUOWOUOWP
      UUGWQWEUUGFWRZUUHSWIWDVKSFWEWIFWEUVAWSWTSWIXATUUGFWQXBXCXEUUGFWCTFXEXDT
      $.
      $( [23-Aug-2014] $) $( [13-May-2013] $)
  $}

  $( The real numbers are equinumerous to their own cross product, even though
     it is not necessarily true that ` RR ` is well-orderable (so we cannot use
     ~ infxpidm2 directly).  (Contributed by Mario Carneiro, 16-Jun-2013.) $)
  rexpen $p |- ( RR X. RR ) ~~ RR $=
    ( cr cxp c2o com cmap co cen wbr cpw cn omex ax-mp entri cdom csdm cfn wcel
    mp2an domentr c0 ovex rpnnen nnenom pwen pw2en xpen xpex 2onn elexi xpmapen
    ensymi wss ssid ssnnfi xpfi isfinite3 canth2 sdomtr sdomdom mapdom1 mapxpen
    mpbi enref xpomen mapen endomtr csn 0ex xpsnen snfi xpdom2 sbth entr4i ) AA
    BZCDEFZACDEUAZVNVOVOBZVOAVOGHZVRVNVQGHADIZVOAJIZVSUBJDGHVTVSGHUCJDKUDLMDKUE
    ZMZWBAVOAVOVPVPUFRVQVONHZVOVQNHZVQVOGHVQCCBZDEFZGHWFVONHZWCWFVQVOVOVPVPUGCC
    DCDUHUIZWHKUJUKWFVODEFZNHZWIVOGHWGWEVONHZWJWEVSNHZVSVOGHZWKWEVSOHZWLWEDOHZD
    VSOHZWNWEPQZWOCPQZWRWQCDQCCULWRUHCUMCCUNRZWSCCUORWEUPVBDKUQZWEDVSURRWEVSUSL
    WAWEVSVOSRWEVODVPKUTLWICDDBZEFZVOCDDWHKKVACCGHXADGHXBVOGHCWHVCVDCCXADWHKVER
    MWFWIVOSRVQWFVOVFRVOVOTVGZBZGHXDVQNHZWDXDVOVPVOTVPVHVIUKXCVONHZXEXCVSNHZWMX
    FXCVSOHZXGXCDOHZWPXHXCPQXITVJXCUPVBWTXCDVSURRXCVSUSLWAXCVSVOSRXCVOVOVPVPVKL
    VOXDVQVFRVQVOVLRMWBVM $.
    $( [16-Jun-2013] $) $( [30-Jul-2004] $)

  ${
    $d v w x y z $.
    $( The complex numbers are equinumerous to the powerset of the natural
       numbers.  (Contributed by Mario Carneiro, 16-Jun-2013.) $)
    cpnnen $p |- CC ~~ ~P NN $=
      ( vv vw vz vx vy cr cc wcel cv wa ci cmul co caddc wceq copab2 reex eleq1
      cvv weq cpw cxp wfo xpex wf1o cmpt2 bi2anan9 oveq12 sylan2 eqeq2d anbi12d
      cn oveq2 cbvoprab12v df-mpt2 eqtr4i cnref1o f1ofo ax-mp fornex mp2 rexpen
      cen wbr f1oen entr3i rpnnen ) FGULUAFFUBZSHVHGAIZFHZBIZFHZJZCIZVIKVKLMZNM
      ZOZJZABCPZUCZGSHFFQQUDZVHGVSUEZVTDEVSVSDIZFHZEIZFHZJZVNWCKWELMZNMZOZJZDEC
      PDEFFWIUFVRWKABCDEADTZBETZJZVMWGVQWJWLVJWDWMVLWFVIWCFRVKWEFRUGWNVPWIVNWMW
      LVOWHOVPWIOVKWEKLUMVIWCVOWHNUHUIUJUKUNDECFFWIUOUPUQZVHGVSURUSVHGSVSUTVAVH
      FGQVBWBVHGVCVDWOVHGVSWAVEUSVFVGVF $.
      $( [16-Jun-2013] $)
  $}

  $( The set of natural numbers is strictly dominated by the set of real
     numbers, i.e. the real numbers are uncountable.  This proof is a simple
     corollary of ~ rpnnen , which determines the exact cardinality of the
     reals.  For an alternate proof discussed at
     ~ http://us.metamath.org/mpegif/mmcomplex.html#uncountable , see
     ~ ruc . $)
  rucALT $p |- NN ~< RR $=
    ( cn cpw csdm wbr cr cen nnex canth2 pwex rpnnen ensymi cvv wcel wa wi reex
    sdomentr ax-mp mp2an ) AABZCDZTEFDZAECDZAGHETAGIJKELMUAUBNUCOPATELQRS $.
    $( [13-May-2013] $) $( [13-Oct-2004] $)

  ${
    $d m w x y $.  $d m x y A $.  $d m x y B $.  $d w z C $.  $d m n x y z F $.
    $d k m n x y z G $.  $d k m n x y M $.  $d k m x y N $.  $d k n w z ph $.
    $d w z D $.  $d n z S $.
    ruc.1 $e |- ( ph -> F : NN --> RR ) $.
    ruc.2 $e |- D = ( x e. ( RR X. RR ) , y e. RR |->
                    [_ ( ( ( 1st ` x ) + ( 2nd ` x ) ) / 2 ) / m ]_
                    if ( m < y , <. ( 1st ` x ) , m >. ,
                         <. ( ( m + ( 2nd ` x ) ) / 2 ) , ( 2nd ` x ) >. ) ) $.
    ${
      ruclem1.3 $e |- ( ph -> A e. RR ) $.
      ruclem1.4 $e |- ( ph -> B e. RR ) $.
      ruclem1.5 $e |- ( ph -> M e. RR ) $.
      ruclem1.6 $e |- X = ( 1st ` ( <. A , B >. D M ) ) $.
      ruclem1.7 $e |- Y = ( 2nd ` ( <. A , B >. D M ) ) $.
      $( Lemma for ~ ruc (the reals are uncountable).  Substitutions for the
         function ` D ` . $)
      ruclem1 $p |- ( ph -> ( ( <. A , B >. D M ) e. ( RR X. RR ) /\
        X = if ( ( ( A + B ) / 2 ) < M , A ,
             ( ( ( ( A + B ) / 2 ) + B ) / 2 ) ) /\
        Y = if ( ( ( A + B ) / 2 ) < M , ( ( A + B ) / 2 ) , B ) ) ) $=
        ( cr wcel cop co cxp caddc c2 cdiv clt wbr cif wceq c1st cfv cv opelxpi
        c2nd csb syl2anc wa ovex simpr breq2d simpl fveq2d opeq1d oveq2d oveq1d
        cvv opeq12d ifbieq12d csbeq2dv mpan2 oveq12d csbeq1d eqtrd opex ovmpt2a
        csbex op1stg op2ndg ax-17 breq1 opeq2 oveq1 csbief biidd syl5eq readdcl
        ifex syl 3eqtrd rehalfcl ifcl eqeltrd fvif op1st sylancl sylancr 3jca
        a1i ) ADEUAZIFUBZSSUCZTJDEUDUBZUEUFUBZIUGUHZDXDEUDUBZUEUFUBZUIZUJKXEXDE
        UIZUJAXAXEDXDUAZXGEUAZUIZXBAXAGWTUKULZWTUOULZUDUBZUEUFUBZGUMZIUGUHZXMXQ
        UAZXQXNUDUBZUEUFUBZXNUAZUIZUPZGXDYCUPZXLAWTXBTZISTXAYDUJADSTZESTZYFNODE
        SSUNUQPBCWTIXBSGBUMZUKULZYIUOULZUDUBZUEUFUBZXQCUMZUGUHZYJXQUAZXQYKUDUBZ
        UEUFUBZYKUAZUIZUPZYDFYIWTUJZYNIUJZURZUUAGYMYCUPZYDUUDYMVGTUUAUUEUJYLUEU
        FUSUUDGYMYTYCVGUUDYOXRYPYSXSYBUUDYNIXQUGUUBUUCUTVAUUDYJXMXQUUDYIWTUKUUB
        UUCVBZVCZVDUUDYRYAYKXNUUDYQXTUEUFUUDYKXNXQUDUUDYIWTUOUUFVCZVEVFUUHVHVIV
        JVKUUDGYMXPYCUUDYLXOUEUFUUDYJXMYKXNUDUUGUUHVLVFVMVNMGXPYCXOUEUFUSXRXSYB
        XMXQVOYAXNVOWHVQVPUQAGXPXDYCAXOXCUEUFAXMDXNEUDAYGXMDUJNDESVRWIZAYGYHXNE
        UJNODESSVSUQZVLVFVMAYEXEXMXDUAZXDXNUDUBZUEUFUBZXNUAZUIZXLGBXDYCUUOXCUEU
        FUSZYIUUOTGVTXQXDUJZXRXEXSYBUUKUUNXQXDIUGWAXQXDXMWBUUQYAUUMXNUUQXTUULUE
        UFXQXDXNUDWCVFVDVIWDAXEXEUUKUUNXJXKAXEWEZAXMDXDUUIVDAUUMXGXNEAUULXFUEUF
        AXNEXDUDUUJVEVFUUJVHVIWFWJZAXJXBTZXKXBTZXLXBTAYGXDSTZUUTNAXCSTZUVBAYGYH
        UVCNODEWGUQXCWKWIZDXDSSUNUQAXGSTZYHUVAAXFSTZUVEAUVBYHUVFUVDOXDEWGUQXFWK
        WIOXGESSUNUQXEXJXKXBWLUQWMAJXAUKULZXHQAUVGXLUKULZXHAXAXLUKUUSVCAUVHXEXJ
        UKULZXKUKULZUIXHXEXJXKUKWNAXEXEUVIUVJDXGUURAYGUVIDUJNDXDSVRWIUVJXGUJAXG
        EXFUEUFUSZWOWSVIWFVNWFAKXAUOULZXIRAUVLXLUOULZXIAXAXLUOUUSVCAUVMXEXJUOUL
        ZXKUOULZUIXIXEXJXKUOWNAXEXEUVNUVOXDEUURAYGXDVGTUVNXDUJNUUPDXDSVGVSWPAXG
        VGTYHUVOEUJUVKOXGEVGSVSWQVIWFVNWFWR $.
        $( [28-May-2014] $)

      ruclem2.8 $e |- ( ph -> A < B ) $.
      $( Lemma for ~ ruc .  Ordering property for the input to ` D ` . $)
      ruclem2 $p |- ( ph -> ( A <_ X /\ X < Y /\ Y <_ B ) ) $=
        ( wbr cle clt caddc co c2 cdiv cif cr wcel syl readdcl syl2anc rehalfcl
        leid wb avglt1 mpbid avglt2 lttrd wi ltle mpd breq2 ifboth cop cxp wceq
        ruclem1 simp2d iftrue breq12d syl5ibrcom iffalse pm2.61d simp3d 3brtr4d
        breqtrrd wn breq1 eqbrtrd 3jca ) ADJUATJKUBTKEUATADDEUCUDZUEUFUDZIUBTZD
        WCEUCUDZUEUFUDZUGZJUAADDUATZDWFUATZDWGUATZADUHUIZWHNDUNUJADWFUBTZWIADWC
        WFNAWBUHUIZWCUHUIZAWKEUHUIZWMNODEUKULWBUMUJZAWEUHUIZWFUHUIZAWNWOWQWPOWC
        EUKULWEUMUJZADEUBTZDWCUBTZSAWKWOWTXAUONODEUPULUQZAWCEUBTZWCWFUBTZAWTXCS
        AWKWOWTXCUONODEURULUQZAWNWOXCXDUOWPOWCEUPULUQUSAWKWRWLWIUTNWSDWFVAULVBW
        DWHWIWJDWFDWGDUAVCWFWGDUAVCVDULADEVEIFUDUHUHVFUIZJWGVGZKWDWCEUGZVGZABCD
        EFGHIJKLMNOPQRVHZVIZVQAWGXHJKUBAWDWGXHUBTZAXLWDXAXBWDWGDXHWCUBWDDWFVJWD
        WCEVJVKVLAXLWDVRZWFEUBTZAXCXNXEAWNWOXCXNUOWPOWCEURULUQXMWGWFXHEUBWDDWFV
        MWDWCEVMVKVLVNXKAXFXGXIXJVOZVPAKXHEUAXOAWCEUATZEEUATZXHEUATZAXCXPXEAWNW
        OXCXPUTWPOWCEVAULVBAWOXQOEUNUJWDXPXQXRWCEWCXHEUAVSEXHEUAVSVDULVTWA $.
        $( [28-May-2014] $)

      $( Lemma for ~ ruc .  The constructed interval ` [ X , Y ] ` always
         excludes ` M ` . $)
      ruclem3 $p |- ( ph -> ( M < X \/ Y < M ) ) $=
        ( clt wbr wn caddc co c2 cdiv wa cle cr wb readdcl syl2anc rehalfcl syl
        wcel lenlt avglt2 mpbid avglt1 wi lelttr syl3anc mpan2d sylbird imp cif
        cop cxp ruclem1 simp2d iffalse sylan9eq breqtrrd ex con1d simp3d iftrue
        wceq simpr eqbrtrd syld orrd ) AIJTUAZKITUAZAWCUBDEUCUDZUEUFUDZITUAZWDA
        WGWCAWGUBZWCAWHUGIWFEUCUDZUEUFUDZJTAWHIWJTUAZAWHIWFUHUAZWKAIUIUOZWFUIUO
        ZWLWHUJPAWEUIUOZWNADUIUOZEUIUOZWONODEUKULWEUMUNZIWFUPULAWLWFWJTUAZWKAWF
        ETUAZWSADETUAZWTSAWPWQXAWTUJNODEUQULURAWNWQWTWSUJWROWFEUSULURAWMWNWJUIU
        OZWLWSUGWKUTPWRAWIUIUOZXBAWNWQXCWROWFEUKULWIUMUNIWFWJVAVBVCVDVEAWHJWGDW
        JVFZWJADEVGIFUDUIUIVHUOZJXDVRZKWGWFEVFZVRZABCDEFGHIJKLMNOPQRVIZVJWGDWJV
        KVLVMVNVOAWGWDAWGUGKWFITAWGKXGWFAXEXFXHXIVPWGWFEVQVLAWGVSVTVNWAWB $.
        $( [28-May-2014] $)
    $}

    ruc.4 $e |- C = ( { <. 0 , <. 0 , 1 >. >. } u. F ) $.
    ruc.5 $e |- G = seq 0 ( D , C ) $.
    $( Lemma for ~ ruc .  Initial value of the interval sequence. $)
    ruclem4 $p |- ( ph -> ( G ` 0 ) = <. 0 , 1 >. ) $=
      ( cc0 cfv c1 cop 0z csn cn0 cn cseq fveq1i cdif cres cun dfn2 reseq2i wfn
      cr wf wceq ffn fnresdm 3syl syl5reqr uneq2d syl5eq fveq1d elexi opex eqid
      cz fvsnun1 syl6eq seq1i ) AMHNMEDMUAZNMOPZMHVFLUBAVGEDMQAMDNMMVGPRZGSMRUC
      ZUDZUEZNVGAMDVKADVHGUEVKKAGVJVHAVJGTUDZGTVIGUFUGATUIGUJGTUHVLGUKITUIGULTG
      UMUNUOUPUQURMVGSGVKMVBQUSMOUTVKVAVCVDVEUQ $.
      $( [28-May-2014] $)

    $( Lemma for ~ ruc .  Domain and range of the interval sequence. $)
    ruclem6 $p |- ( ph -> G : NN0 --> ( RR X. RR ) ) $=
      ( cr cc0 cfv c1 wcel wceq co cn vz vw cn0 cxp cseq wf cop fveq1i cz ax-mp
      0z seq1 eqtri ruclem4 syl5eqr 0re 1re opelxpi mp2an syl6eqel cv c1st c2nd
      wa 1st2nd2 ad2antrl oveq1d caddc c2 clt wbr cif adantr xp1st xp2nd simprr
      cdiv eqid ruclem1 simp1d eqeltrd nn0uz a1i cuz ax-1cn addid2i fveq2i nnuz
      eqtr4i eleq2i csn cun equncomi wne nnne0 necomd fvunsn syl syl5eq ffvelrn
      adantl sylan sylan2b seqf2 feq1i sylibr ) AUCMMUDZEDNUEZUFUCXGHUFAUAUBXGM
      EDNUCANDOZNPUGZXGAXINHOZXJXKNXHOZXINHXHLUHNUIQZXLXIRUKEDNULUJUMABCDEFGHIJ
      KLUNUONMQPMQXJXGQUPUQNPMMURUSUTAUAVAZXGQZUBVAZMQZVDZVDZXNXPESXNVBOZXNVCOZ
      UGZXPESZXGXSXNYBXPEXOXNYBRAXQXNMMVEVFVGXSYCXGQYCVBOZXTYAVHSVIVQSZXPVJVKZX
      TYEYAVHSVIVQSVLRYCVCOZYFYEYAVLRXSBCXTYAEFGXPYDYGATMGUFZXRIVMJXOXTMQAXQXNM
      MVNVFXOYAMQAXQXNMMVOVFAXOXQVPYDVRYGVRVSVTWAWBXMAUKWCXNNPVHSZWDOZQAXNTQZXN
      DOZMQYJTXNYJPWDOTYIPWDPWEWFWGWHWIWJAYKVDYLXNGOZMYKYLYMRAYKYLXNGNXJUGWKZWL
      ZOZYMXNDYODYNGKWMUHYKNXNWNYPYMRYKXNNXNWOWPGNXJXNWQWRWSXAAYHYKYMMQITMXNGWT
      XBWAXCXDUCXGHXHLXEXF $.
      $( [28-May-2014] $)

    $( Lemma for ~ ruc .  Successor value for the interval sequence. $)
    ruclem7 $p |- ( ( ph /\ N e. NN0 ) ->
      ( G ` ( N + 1 ) ) = ( ( G ` N ) D ( F ` ( N + 1 ) ) ) ) $=
      ( cn0 wcel co cfv cc0 syl fveq1i wa c1 caddc cseq cuz wceq simpr syl6eleq
      nn0uz seqp1 oveq1i 3eqtr4g wne cn nn0p1nn adantl nnne0 necomd cop csn cun
      equncomi fvunsn syl5eq oveq2d eqtrd ) AINOZUAZIUBUCPZHQZIHQZVIDQZEPZVKVIG
      QZEPVHVIEDRUDZQZIVOQZVLEPZVJVMVHIRUEQZOVPVRUFVHINVSAVGUGUIUHEDRIUJSVIHVOM
      TVKVQVLEIHVOMTUKULVHVLVNVKEVHRVIUMZVLVNUFVHVIRVHVIUNOZVIRUMVGWAAIUOUPVIUQ
      SURVTVLVIGRRUBUSZUSUTZVAZQVNVIDWDDWCGLVBTGRWBVIVCVDSVEVF $.
      $( [28-May-2014] $)

    $( Lemma for ~ ruc .  The intervals of the ` G ` sequence are all
       nonempty. $)
    ruclem8 $p |- ( ( ph /\ N e. NN0 ) ->
                    ( 1st ` ( G ` N ) ) < ( 2nd ` ( G ` N ) ) ) $=
      ( cfv c1st c2nd clt cc0 fveq2d cr vk vn cn0 wcel wbr cv wi c1 caddc fveq2
      co wceq breq12d imbi2d lt01 a1i ruclem4 cz 0z elexi op1st syl6eq 1z op2nd
      cop 3brtr4d wa cle cn wf adantr ruclem6 ffvelrn sylan adantrr xp1st xp2nd
      cxp syl nn0p1nn syl2an simprr ruclem2 simp2d ruclem7 1st2nd2 oveq1d eqtrd
      eqid expr expcom a2d nn0ind impcom ) IUCUDAIHNZONZWOPNZQUEZAUAUFZHNZONZWT
      PNZQUEZUGARHNZONZXDPNZQUEZUGAUBUFZHNZONZXIPNZQUEZUGAXHUHUIUKZHNZONZXNPNZQ
      UEZUGAWRUGUAUBIWSRULZXCXGAXRXAXEXBXFQXRWTXDOWSRHUJZSXRWTXDPXSSUMUNWSXHULZ
      XCXLAXTXAXJXBXKQXTWTXIOWSXHHUJZSXTWTXIPYASUMUNWSXMULZXCXQAYBXAXOXBXPQYBWT
      XNOWSXMHUJZSYBWTXNPYCSUMUNWSIULZXCWRAYDXAWPXBWQQYDWTWOOWSIHUJZSYDWTWOPYES
      UMUNARUHXEXFQRUHQUEAUOUPAXERUHVEZONRAXDYFOABCDEFGHJKLMUQZSRUHRURUSUTZVAVB
      AXFYFPNUHAXDYFPYGSRUHYHUHURVCUTVDVBVFXHUCUDZAXLXQAYIXLXQUGAYIXLXQAYIXLVGZ
      VGZXJXKVEZXMGNZEUKZONZYNPNZXOXPQYKXJYOVHUEYOYPQUEYPXKVHUEYKBCXJXKEFGYMYOY
      PAVITGVJZYJJVKKYKXITTVRZUDZXJTUDAYIYSXLAUCYRHVJYIYSABCDEFGHJKLMVLUCYRXHHV
      MVNVOZXITTVPVSYKYSXKTUDYTXITTVQVSAYIYMTUDZXLAYQXMVIUDUUAYIJXHVTVITXMGVMWA
      VOYOWIYPWIAYIXLWBWCWDYKXNYNOYKXNXIYMEUKZYNAYIXNUUBULXLABCDEFGHXHJKLMWEVOY
      KXIYLYMEYKYSXIYLULYTXITTWFVSWGWHZSYKXNYNPUUCSVFWJWKWLWMWN $.
      $( [28-May-2014] $)

    ${
      ruclem9.6 $e |- ( ph -> M e. NN0 ) $.
      ruclem9.7 $e |- ( ph -> N e. ( ZZ>= ` M ) ) $.
      $( Lemma for ~ ruc .  The first components of the ` G ` sequence are
         increasing, and the second components are decreasing. $)
      ruclem9 $p |- ( ph -> ( ( 1st ` ( G ` M ) ) <_ ( 1st ` ( G ` N ) ) /\
                              ( 2nd ` ( G ` N ) ) <_ ( 2nd ` ( G ` M ) ) ) ) $=
        ( cfv wcel cle cr vk vn cuz c1st wbr c2nd wa cv wi c1 caddc wceq fveq2d
        co fveq2 breq2d breq1d anbi12d imbi2d cz cxp wf ruclem6 ffvelrn syl2anc
        cn0 xp1st syl leid xp2nd jca a1i cop clt cn adantr eluznn0 nn0p1nn eqid
        ruclem8 syldan ruclem2 simp1d ruclem7 1st2nd2 oveq1d breqtrrd peano2nn0
        sylan eqtrd letr syl3anc mpan2d simp3d eqbrtrd mpand anim12d expcom a2d
        uzind4 mpcom ) JIUCQZRAIHQZUDQZJHQZUDQZSUEZXEUFQZXCUFQZSUEZUGZPAXDUAUHZ
        HQZUDQZSUEZXMUFQZXISUEZUGZUIAXDXDSUEZXIXISUEZUGZUIZAXDUBUHZHQZUDQZSUEZY
        DUFQZXISUEZUGZUIAXDYCUJUKUNZHQZUDQZSUEZYKUFQZXISUEZUGZUIAXKUIUAUBIJXLIU
        LZXRYAAYQXOXSXQXTYQXNXDXDSYQXMXCUDXLIHUOZUMUPYQXPXIXISYQXMXCUFYRUMUQURU
        SXLYCULZXRYIAYSXOYFXQYHYSXNYEXDSYSXMYDUDXLYCHUOZUMUPYSXPYGXISYSXMYDUFYT
        UMUQURUSXLYJULZXRYPAUUAXOYMXQYOUUAXNYLXDSUUAXMYKUDXLYJHUOZUMUPUUAXPYNXI
        SUUAXMYKUFUUBUMUQURUSXLJULZXRXKAUUCXOXGXQXJUUCXNXFXDSUUCXMXEUDXLJHUOZUM
        UPUUCXPXHXISUUCXMXEUFUUDUMUQURUSYBIUTRAXSXTAXDTRZXSAXCTTVAZRZUUEAVFUUFH
        VBZIVFRZUUGABCDEFGHKLMNVCZOVFUUFIHVDVEZXCTTVGVHZXDVIVHAXITRZXTAUUGUUMUU
        KXCTTVJVHZXIVIVHVKVLYCXBRZAYIYPAUUOYIYPUIAUUOUGZYFYMYHYOUUPYFYEYLSUEZYM
        UUPYEYEYGVMZYJGQZEUNZUDQZYLSUUPYEUVASUEZUVAUUTUFQZVNUEZUVCYGSUEZUUPBCYE
        YGEFGUUSUVAUVCAVOTGVBZUUOKVPZLUUPYDUUFRZYETRZUUPUUHYCVFRZUVHAUUHUUOUUJV
        PZAUUIUUOUVJOYCIVQWIZVFUUFYCHVDVEZYDTTVGVHZUUPUVHYGTRZUVMYDTTVJVHZUUPUV
        FYJVORZUUSTRUVGUUPUVJUVQUVLYCVRVHVOTYJGVDVEUVAVSUVCVSAUUOUVJYEYGVNUEUVL
        ABCDEFGHYCKLMNVTWAWBZWCUUPYKUUTUDUUPYKYDUUSEUNZUUTAUUOUVJYKUVSULUVLABCD
        EFGHYCKLMNWDWAUUPYDUURUUSEUUPUVHYDUURULUVMYDTTWEVHWFWJZUMWGUUPUUEUVIYLT
        RZYFUUQUGYMUIAUUEUUOUULVPUVNUUPYKUUFRZUWAUUPUUHYJVFRZUWBUVKUUPUVJUWCUVL
        YCWHVHVFUUFYJHVDVEZYKTTVGVHXDYEYLWKWLWMUUPYNYGSUEZYHYOUUPYNUVCYGSUUPYKU
        UTUFUVTUMUUPUVBUVDUVEUVRWNWOUUPYNTRZUVOUUMUWEYHUGYOUIUUPUWBUWFUWDYKTTVJ
        VHUVPAUUMUUOUUNVPYNYGXIWKWLWPWQWRWSWTXA $.
        $( [28-May-2014] $)
    $}

    ${
      ruclem10.6 $e |- ( ph -> M e. NN0 ) $.
      ruclem10.7 $e |- ( ph -> N e. NN0 ) $.
      $( Lemma for ~ ruc .  Every first component of the ` G ` sequence is less
         than every second component.  That is, the sequences form a chain a_1
         ` < ` a_2 ` < ` ... ` < ` b_2 ` < ` b_1, where a_i are the first
         components and b_i are the second components. $)
      ruclem10 $p |- ( ph -> ( 1st ` ( G ` M ) ) < ( 2nd ` ( G ` N ) ) ) $=
        ( cfv cr wcel syl c1st cle wbr cif c2nd cxp cn0 ruclem6 ffvelrn syl2anc
        wf xp1st ifcl xp2nd cuz nn0re max1 cz wb nn0z mpbird ruclem9 simpld clt
        eluz ruclem8 mpdan max2 simprd ltletrd lelttrd ) AIHQZUAQZIJUBUCZJIUDZH
        QZUAQZJHQZUEQZAVLRRUFZSZVMRSAUGVTHUKZIUGSZWAABCDEFGHKLMNUHZOUGVTIHUIUJV
        LRRULTAVPVTSZVQRSAWBVOUGSZWEWDAJUGSZWCWFPOVNJIUGUMUJZUGVTVOHUIUJZVPRRUL
        TZAVRVTSZVSRSAWBWGWKWDPUGVTJHUIUJVRRRUNTZAVMVQUBUCVPUEQZVLUEQUBUCABCDEF
        GHIVOKLMNOAVOIUOQSZIVOUBUCZAIRSZJRSZWOAWCWPOIUPTZAWGWQPJUPTZIJUQUJAIURS
        ZVOURSZWNWOUSAWCWTOIUTTAWFXAWHVOUTTZIVOVEUJVAVBVCAVQWMVSWJAWEWMRSWIVPRR
        UNTWLAWFVQWMVDUCWHABCDEFGHVOKLMNVFVGAVRUAQVQUBUCWMVSUBUCABCDEFGHJVOKLMN
        PAVOJUOQSZJVOUBUCZAWPWQXDWRWSIJVHUJAJURSZXAXCXDUSAWGXEPJUTTXBJVOVEUJVAV
        BVIVJVK $.
        $( [28-May-2014] $)
    $}

    $( Lemma for ~ ruc .  Closure lemmas for supremum. $)
    ruclem11 $p |- ( ph -> ( ran ( 1st o. G ) C_ RR /\ ran ( 1st o. G ) =/= (/)
                             /\ A. z e. ran ( 1st o. G ) z <_ 1 ) ) $=
      ( c1st cr c0 c1 cn0 syl cc0 vn ccom crn wss wne cv cle wbr wf cxp ruclem6
      wral 1stcof frn cdm wceq fdm wcel 0nn0 ax-mp a1i eqnetrd dm0rn0 necon3bii
      ne0i sylib cfv wa wfun cvv wfo fo1st fofun adantr simpr fvco3 syl3anc clt
      c2nd cn ruclem10 cop ruclem4 fveq2d elexi op2nd syl6eq breqtrd wi ffvelrn
      1re sylan xp1st ltle sylancl mpd eqbrtrd ralrimiva wfn wb ffn breq1 ralrn
      mpbird 3jca ) ANIUBZUCZOUDZXGPUEZDUFZQUGUHZDXGULZAROXFUIZXHAROOUJZIUIZXMA
      BCEFGHIJKLMUKZROOIUMSZROXFUNSAXFUOZPUEXIAXRRPAXMXRRUPXQROXFUQSRPUEZATRURZ
      XSUSRTVEUTVAVBXRPXGPXFVCVDVFAXLUAUFZXFVGZQUGUHZUARULZAYCUARAYARURZVHZYBYA
      IVGZNVGZQUGYFNVIZXOYEYBYHUPYIYFVJVJNVKYIVLVJVJNVMUTVAAXOYEXPVNAYEVOZRXNYA
      NIVPVQYFYHQVRUHZYHQUGUHZYFYHTIVGZVSVGZQVRYFBCEFGHIYATAVTOHUIYEJVNKLMYJXTY
      FUSVAWAAYNQUPYEAYNTQWBZVSVGQAYMYOVSABCEFGHIJKLMWCWDTQTRUSWEQOWKWEWFWGVNWH
      YFYHOURZQOURYKYLWIYFYGXNURZYPAXOYEYQXPRXNYAIWJWLYGOOWMSWKYHQWNWOWPWQWRAXF
      RWSZXLYDWTAXMYRXQROXFXASXKYCDUARXFXJYBQUGXBXCSXDXE $.
      $( [28-May-2014] $)

    ruc.6 $e |- S = sup ( ran ( 1st o. G ) , RR , < ) $.
    $( Lemma for ~ ruc .  The supremum of the increasing sequence ` 1st o. G `
       is a real number that is not in the range of ` F ` . $)
    ruclem12 $p |- ( ph -> S e. ( RR \ ran F ) ) $=
      ( vz cr wcel wbr cfv cn0 vn vk crn wn cdif c1st ccom clt csup wss wne cle
      c0 cv wral wrex c1 ruclem11 simp1d simp2d 1re simp3d wceq ralbidv rcla4ev
      breq2 sylancr suprcl syl3anc syl5eqel cn wa wo c2nd cmin co cop wf adantr
      cxp ruclem6 nnm1nn0 ffvelrn syl2an xp1st xp2nd sylan eqid ruclem8 ruclem3
      syl sylan2 caddc ruclem7 nncn adantl ax-1cn npcan sylancl 1st2nd2 oveq12d
      cc fveq2d 3eqtr3d breq2d breq1d orbi12d mpbird wfun cvv fo1st fofun ax-mp
      wfo a1i nnnn0 fvco3 wfn 1stcof ffn 3syl fnfvelrn eqeltrrd suprub syl31anc
      syl2anc syl6breqr wi ltletr mpan2d mp3an1 simpr ruclem10 ltle mpd eqbrtrd
      ralrimiva wb breq1 ralrn suprleub lelttr mpand orim12d lttri2 df-ne sylib
      syl5eqbr nrexdv risset eqeq1 rexrn syl5bb mtbird eldif sylanbrc ) AFPQZFH
      UCZQZUDFPUURUEQAFUFIUGZUCZPUHUIZPNAUVAPUJZUVAUMUKZOUNZUAUNZULRZOUVAUOZUAP
      UPZUVBPQAUVCUVDUVEUQULRZOUVAUOZABCODEGHIJKLMURZUSZAUVCUVDUVKUVLUTZAUQPQUV
      KUVIVAAUVCUVDUVKUVLVBUVHUVKUAUQPUVFUQVCUVGUVJOUVAUVFUQUVEULVFVDVEVGZUAOUV
      AVHVIVJZAUUSUVFHSZFVCZUAVKUPZAUVRUAVKAUVFVKQZVLZUVQFUKZUVRUDUWAUWBUVQFUHR
      ZFUVQUHRZVMZUWAUVQUVFISZUFSZUHRZUWFVNSZUVQUHRZVMZUWEUWAUWKUVQUVFUQVOVPZIS
      ZUFSZUWMVNSZVQZUVQEVPZUFSZUHRZUWQVNSZUVQUHRZVMUWABCUWNUWOEGHUVQUWRUWTAVKP
      HVRZUVTJVSZKUWAUWMPPVTZQZUWNPQATUXDIVRZUWLTQZUXEUVTABCDEGHIJKLMWAZUVFWBZT
      UXDUWLIWCWDZUWMPPWEWKUWAUXEUWOPQUXJUWMPPWFWKAUXBUVTUVQPQZJVKPUVFHWCWGZUWR
      WHUWTWHUVTAUXGUWNUWOUHRUXIABCDEGHIUWLJKLMWIWLWJUWAUWHUWSUWJUXAUWAUWGUWRUV
      QUHUWAUWFUWQUFUWAUWLUQWMVPZISZUWMUXMHSZEVPZUWFUWQUVTAUXGUXNUXPVCUXIABCDEG
      HIUWLJKLMWNWLUWAUXMUVFIUWAUVFXBQZUQXBQUXMUVFVCUVTUXQAUVFWOWPWQUVFUQWRWSZX
      CUWAUWMUWPUXOUVQEUWAUXEUWMUWPVCUXJUWMPPWTWKUWAUXMUVFHUXRXCXAXDZXCXEUWAUWI
      UWTUVQUHUWAUWFUWQVNUXSXCXFXGXHUWAUWHUWCUWJUWDUWAUWHUWGFULRZUWCUWAUWGUVBFU
      LUWAUVCUVDUVIUWGUVAQUWGUVBULRAUVCUVTUVMVSZAUVDUVTUVNVSZAUVIUVTUVOVSZUWAUV
      FUUTSZUWGUVAUWAUFXIZUXFUVFTQZUYDUWGVCUYEUWAXJXJUFXNUYEXKXJXJUFXLXMZXOAUXF
      UVTUXHVSZUVTUYFAUVFXPZWPZTUXDUVFUFIXQVIUWAUUTTXRZUYFUYDUVAQUWAUXFTPUUTVRU
      YKUYHTPPIXSTPUUTXTYAZUYJTUVFUUTYBYFYCUAOUVAUWGYDYENYGUWAUXKUWGPQZUUQUWHUX
      TVLUWCYHUXLUWAUWFUXDQZUYMAUXFUYFUYNUVTUXHUYITUXDUVFIWCWDZUWFPPWEWKAUUQUVT
      UVPVSZUVQUWGFYIVIYJUWAFUWIULRZUWJUWDUWAFUVBUWIULNUWAUVBUWIULRZUVEUWIULRZO
      UVAUOZUWAUYTUBUNZUUTSZUWIULRZUBTUOZUWAVUCUBTUWAVUATQZVLZVUBVUAISZUFSZUWIU
      LUWAUXFVUEVUBVUHVCZUYHUYEUXFVUEVUIUYGTUXDVUAUFIXQYKWGVUFVUHUWIUHRZVUHUWIU
      LRZVUFBCDEGHIVUAUVFUWAUXBVUEUXCVSKLMUWAVUEYLUWAUYFVUEUYJVSYMVUFVUHPQZUWIP
      QZVUJVUKYHVUFVUGUXDQZVULUWAUXFVUEVUNUYHTUXDVUAIWCWGVUGPPWEWKUWAVUMVUEUWAU
      YNVUMUYOUWFPPWFWKZVSVUHUWIYNYFYOYPYQUWAUYKUYTVUDYRUYLUYSVUCOUBTUUTUVEVUBU
      WIULYSYTWKXHUWAUVCUVDUVIVUMUYRUYTYRUYAUYBUYCVUOUAOOUVAUWIUUAYEXHUUHUWAUUQ
      VUMUXKUYQUWJVLUWDYHUYPVUOUXLFUWIUVQUUBVIUUCUUDYOUWAUXKUUQUWBUWEYRUXLUYPUV
      QFUUEYFXHUVQFUUFUUGUUIUUSUVEFVCZOUURUPZAUVSOFUURUUJAUXBHVKXRVUQUVSYRJVKPH
      XTVUPUVROUAVKHUVEUVQFUUKUULYAUUMUUNFPUURUUOUUP $.
      $( [28-May-2014] $)

  $}

  ${
    $d k m w x y z $.  $d k w z F $.
    $( Lemma for ~ ruc .  There is no function that maps ` NN ` onto ` RR ` .
       (Use ~ nex if you want this in the form
       ` -. E. f f : NN -onto-> RR ` .) $)
    ruclem13 $p |- -. F : NN -onto-> RR $=
      ( vx vy vm vz vk cr wceq c1st cv cfv caddc co c2 cdiv clt cop cif csb cvv
      vw cn wfo crn cdif c0 forn difeq2d difid syl6eq cxp c2nd wbr cmpt2 cc0 c1
      csn cun cseq ccom csup wcel fof breq1 opeq2 oveq1 oveq1d opeq1d ifbieq12d
      wn ovex cbvcsbv ax-mp fveq2 oveq12d csbeq1d biidd oveq2d opeq12d csbeq2dv
      mpan2 eqtrd syl5eq breq2 ifbid cbvmpt2v eqid ruclem12 n0i syl pm2.65i ) U
      BGAUCZGAUDZUEZUFHZWLWNGGUEUFWLWMGGUBGAUGUHGUIUJWLIBCGGUKZGDBJZIKZWQULKZLM
      ZNOMZDJZCJZPUMZWRXBQZXBWSLMZNOMZWSQZRZSZUNZUOUOUPQQUQAURZUOUSZUTUDGPVAZWN
      VBWOVJWLEUAXLXKXNFAXMUBGAVCBCEUAWPGXJFEJZIKZXOULKZLMZNOMZFJZUAJZPUMZXPXTQ
      ZXTXQLMZNOMZXQQZRZSZFXSXTXCPUMZYCYFRZSZWQXOHZXJFXAYIWRXTQZXTWSLMZNOMZWSQZ
      RZSZYKXATVBXJYRHWTNOVKDFXAXIYQTXBXTHZXDYIXEXHYMYPXBXTXCPVDXBXTWRVEYSXGYOW
      SYSXFYNNOXBXTWSLVFVGVHVIVLVMYLYRFXSYQSZYKYLFXAXSYQYLWTXRNOYLWRXPWSXQLWQXO
      IVNZWQXOULVNZVOVGVPYLXSTVBZYTYKHXRNOVKZYLFXSYQYJTYLYIYIYMYPYCYFYLYIVQYLWR
      XPXTUUAVHYLYOYEWSXQYLYNYDNOYLWSXQXTLUUBVRVGUUBVSVIVTWAWBWCXCYAHZUUCYKYHHU
      UDUUEFXSYJYGTUUEYIYBYCYFXCYAXTPWDWEVTWAWFXLWGXMWGXNWGWHWNXNWIWJWK $.
      $( [28-May-2014] $) $( [14-Oct-2004] $)
  $}

  $( The set of natural numbers is strictly dominated by the set of real
     numbers, i.e. the real numbers are uncountable.  The proof consists of
     lemmas ~ ruclem1 through ~ ruclem13 and this final piece.  Our proof is
     based on the proof of Theorem 5.18 of [Truss] p. 114.  See ~ ruclem13 for
     the function existence version of this theorem.  For an informal
     discussion of this proof, see
     ~ http://us.metamath.org/mpegif/mmcomplex.html#uncountable .  For an
     alternate proof see ~ rucALT . $)
  ruc $p |- NN ~< RR $=
    ( vf cn cr csdm wbr cdom cen wn brsdom cvv wcel wss reex nnssre ssdom2g mp2
    cv wf1o wex wfo ruclem13 f1ofo mto nex bren mtbir mpbir2an ) BCDEBCFEZBCGEZ
    HBCICJKBCLUHMNBCJOPUIBCAQZRZASUKAUKBCUJTUJUABCUJUBUCUDBCAMUEUFUG $.
    $( [13-Oct-2004] $)

  $( The set of rationals is strictly less equinumerous than the set of reals
     ( ` RR ` strictly dominates ` QQ ` ). $)
  resdomq $p |- QQ ~< RR $=
    ( cq cn cen wbr cr csdm qnnen ruc ensdomtr mp2an ) ABCDBEFDAEFDGHABEIJ $.
    $( [18-Dec-2004] $)

  $( There are at least aleph-one real numbers. $)
  aleph1re $p |- ( aleph ` 1o ) ~<_ RR $=
    ( c1o cale cfv cr cdom wbr csdm wn c0 csuc cen com aleph0 omex nnenom mp2an
    cn mpbir cvv wcel ensymi eqbrtri ruc ensdomtr wi wa alephnbtwn2 imnan ax-mp
    df-1o fveq2i breq2i mtbir wb fvex reex domtri ) ABCZDEFZDURGFZHZUTDIJZBCZGF
    ZIBCZDGFZVDHZVEQKFQDGFVFVELQKMQLNOUAUBUCVEQDUDPVFVGUEVFVDUFHIDUGVFVDUHRUIUR
    VCDGAVBBUJUKULUMURSTDSTUSVAUNABUOUPURDSSUQPR $.
    $( [2-Feb-2005] $)

  $( There are at least aleph-one irrationals. $)
  aleph1irr $p |- ( aleph ` 1o ) ~<_ ( RR \ QQ ) $=
    ( c1o cale cfv cr cdom wbr cq cdif cen aleph1re reex com csdm nnenom ensymi
    cn omex ruc ensdomtr mp2an sdomdom ax-mp resdomq qex infdif domentr ) ABCZD
    EFDDGHZIFUGUHEFJUHDKLDEFZGDMFUHDIFLDMFZUILPIFPDMFUJPLQNORLPDSTLDUAUBUCDGKUD
    UETOUGDUHUFT $.
    $( [2-Feb-2005] $)

