$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               The ` # ` (finite set size) function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c # $.

  $( Extend the definition of a class to include the size function. $)
  chash $a class # $.

  ${
    $d x y $.
    $( Define the ` # ` function, which gives the cardinality of a finite set
       as a member of ` NN0 ` , and assigns all infinite sets the value
       ` +oo ` . $)
    df-hash $a |- # = ( ( ( rec (
                   ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) o. card ) u.
                   ( ( _V \ Fin ) X. { +oo } ) ) $.
  $}

  ${
    $d A x y $.  $d K y $.
    hashgval.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    ${
      hashkf.2 $e |- K = ( G o. card ) $.
      $( The finite part of the size function maps all finite sets to their
         cardinality, as members of ` NN0 ` .  (The proof was shortened by
         Mario Carneiro, 13-Jul-2014.) $)
      hashkf $p |- K : Fin --> NN0 $=
        ( vy cfn cn0 wf wfn cfv wcel ccrd wfun wceq com ax-mp cen wbr con0 wral
        cv ffnfv ccom cdm df-fn cvv c1 caddc co cmpt cc0 crdg cres frfnom mpbir
        fneq1i fnfun wrex cab cardf2 ffun funco mp2an ccnv cima dmco imaeq2i wa
        fndm wb ffn elpreima abid anbi1i id cardid2 vex ensym syl breq2 rcla4ev
        syl2anr isfi sylibr finnum ficardom impbii 3bitri eqriv 3eqtri mpbir2an
        fveq1i fvco3 mp3an12 syl5eq wf1o hashgf1o f1of ffvelrni eqeltrd rgen
        jca ) GHCICGJZFUBZCKZHLZFGUAFGHCUCXDBMUDZGJZXIXHNZXHUEZGOXHGUFBNZMNZXJB
        PJZXLXNAUGAUBZUHUIUJUKZULUMPUNZPJULXPUOPBXQDUQUPZPBURQZXOXERSATUSZFUTZT
        MIZXMFAVAZYATMVBQBMVCVDXKMVEZBUEZVFYDPVFZGBMVGYEPYDXNYEPOXRPBVJQVHFYFGX
        EYFLZXEYALZXEMKZPLZVIZXTYJVIZXEGLZMYAJZYGYKVKYBYNYCYATMVLQYAXEPMVMQYHXT
        YJXTFVNZVOYLYMYLXEXORSZAPUSZYMYJYJXEYIRSZYQXTYJVPXTYIXERSYRAXEVQYIXEFVR
        VSVTYPYRAYIPXOYIXERWAWBWCAXEWDWEYMXTYJAXEWFZXEWGZXCWHWIWJWKWLGCXHEUQUPX
        GFGYMXFYIBKZHYMXFXEXHKZUUAXECXHEWMYMYHUUBUUAOZYMXTYHYSYOWEXLYBYHUUCXSYC
        YATXEBMWNWOVTWPYMYJUUAHLYTPHYIBPHBWQPHBIABDWRPHBWSQWTVTXAXBWL $.
        $( [13-Jul-2014] $) $( [13-Sep-2013] $)
    $}

    $( The value of the ` # ` function in terms of the mapping ` G ` from
       ` om ` to ` NN0 ` .  The proof avoids the use of ~ ax-ac .  (Contributed
       by Paul Chapman, 22-Jun-2011.)  (Revised by Mario Carneiro,
       12-Feb-2013.) $)
    hashgval $p |- ( A e. Fin -> ( G ` ( card ` A ) ) = ( # ` A ) ) $=
      ( vy cfn chash cres cfv ccrd cvv com cpnf cun c0 wf wfn wceq ax-mp con0
      wcel ccom cv c1 caddc cmpt cc0 crdg cdif csn cxp resundir cn0 eqid hashkf
      co ffn fnresdm mp2b cin incom disjdif eqtri wb cxr pnfxr fconst fnresdisj
      mpbi uneq12i un0 3eqtri df-hash reseq1 coeq1i 3eqtr4i fveq1i cen wbr wrex
      elexi cab finnum breq2 elabg mpbird wfun frfnom fneq1i mpbir fnfun cardf2
      rexbidv fvco3 mp3an12 syl syl5eq fvres eqtr3d ) BFUAZBGFHZIZBJICIZBGIWTXB
      BCJUBZIZXCBXAXDAKAUCZUDUEUPUFZUGUHLHZJUBZKFUIZMUJZUKZNZFHZXIXAXDXNXIFHZXL
      FHZNXIONXIXIXLFULXOXIXPOFUMXIPXIFQXOXIRAXHXIXHUNXIUNUOFUMXIUQFXIURUSXJFUT
      ZORZXPORZXQFXJUTOXJFVAFKVBVCXLXJQZXRXSVDXJXKXLPXTXJMMVEVFWAVGXJXKXLUQSXJF
      XLVHSVIVJXIVKVLGXMRXAXNRAVMGXMFVNSCXHJDVOVPVQWTBEUCZXFVRVSZETVTZAWBZUAZXE
      XCRZWTYEYABVRVSZETVTZEBWCYCYHABFXFBRYBYGETXFBYAVRWDWMWEWFCWGZYDTJPYEYFCLQ
      ZYIYJXHLQUGXGWHLCXHDWIWJLCWKSAEWLYDTBCJWNWOWPWQBFGWRWS $.
      $( [13-Jul-2014] $) $( [22-Jun-2011] $)

    $( ` ``' G ` maps the size function's value to ` card ` .  (Contributed by
       Paul Chapman, 22-Jun-2011.) $)
    hashginv $p |- ( A e. Fin -> ( `' G ` ( # ` A ) ) = ( card ` A ) ) $=
      ( cfn wcel ccrd cfv chash wceq ccnv hashgval com wi ficardom cn0 hashgf1o
      wf1o f1ocnvfv mpan syl mpd ) BEFZBGHZCHBIHZJZUECKHUDJZABCDLUCUDMFZUFUGNZB
      OMPCRUHUIACDQMPUDUECSTUAUB $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  ${
    $d A x y $.
    $( The value of the ` # ` function on an infinite set. $)
    hashinf $p |- ( A e. ( V \ Fin ) -> ( # ` A ) = +oo ) $=
      ( vx cfn cdif wcel cvv chash cfv cpnf wceq wss ax-mp cres cun c0 cn0 eqid
      wf wfn ssv ssdif sseli fvres csn cxp cv caddc cmpt cc0 crdg com ccrd ccom
      c1 df-hash reseq1 resundir cin disjdif hashkf ffn fnresdisj mp2b mpbi cxr
      co wb pnfxr elexi fconst fnresdm uneq12i uncom un0 3eqtri fveq1i fvconst2
      syl5eq eqtr3d syl ) ABDEZFAGDEZFZAHIZJKWBWCABGLWBWCLBUABGDUBMUCWDAHWCNZIZ
      WEJAWCHUDWDWGAWCJUEZUFZIJAWFWIWFCGCUGUOUHVGUIUJUKULNZUMUNZWIOZWCNZWKWCNZW
      IWCNZOZWIHWLKWFWMKCUPHWLWCUQMWKWIWCURWPPWIOWIPOWIWNPWOWIDWCUSPKZWNPKZDGUT
      DQWKSWKDTWQWRVHCWJWKWJRWKRVADQWKVBDWCWKVCVDVEWCWHWISWIWCTWOWIKWCJJVFVIVJZ
      VKWCWHWIVBWCWIVLVDVMPWIVNWIVOVPVPVQWCJAWSVRVSVTWA $.
      $( [13-Jul-2014] $)
  $}

  ${
    $d x y z $.
    $( The size function maps all finite sets to their cardinality, as members
       of ` NN0 ` , and infinite sets to ` +oo ` . $)
    hashf $p |- # : _V --> ( NN0 u. { +oo } ) $=
      ( vx cvv cn0 cpnf csn cun chash wf cfn cdif cv c1 caddc cmpt cc0 crdg com
      co cres eqid ccrd ccom cxp wa cin c0 hashkf cxr pnfxr elexi fconst pm3.2i
      wceq disjdif fun mp2an df-hash feq1i undifv feq2i bitr4i mpbir ) BCDEZFZG
      HZIBIJZFZVDABAKLMRNOPQSZUAUBZVFVCUCZFZHZICVIHZVFVCVJHZUDIVFUEUFUMVLVMVNAV
      HVIVHTVITUGVFDDUHUIUJUKULIBUNIVFCVCVIVJUOUPVEBVDVKHVLBVDGVKAUQURVGBVDVKIU
      SUTVAVB $.
      $( [13-Jul-2014] $) $( [13-Sep-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d x N $.
    $( The set ` ( 1 ... N ) ` has ` N ` elements.  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    hashfz1 $p |- ( N e. NN0 -> ( # ` ( 1 ... N ) ) = N ) $=
      ( vx cn0 wcel c1 cfz co ccrd cfv cvv cv caddc cmpt cc0 crdg com cres ccnv
      chash wceq eqid cardfz fveq2d fzfid hashgval wf1o hashgf1o f1ocnvfv2 mpan
      cfn syl 3eqtr3d ) ACDZEAFGZHIZBJBKELGMNOPQZIZAUPRIZUPIZUNSIZAUMUOURUPBUPA
      UPUAZUBUCUMUNUJDUQUTTUMEAUDBUNUPVAUEUKPCUPUFUMUSATBUPVAUGPCAUPUHUIUL $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)

    $( Two finite sets have the same number of elements iff they are
       equinumerous.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    hashen $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( ( # ` A ) = ( # ` B ) <-> A ~~ B ) ) $=
      ( vx cfn wcel chash cfv wceq ccrd cen wbr fveq2 hashginv eqeqan12d syl5ib
      wa hashgval impbid wi con0 cvv cv c1 caddc co cmpt cc0 crdg com cres ccnv
      eqid wrex finnum cardid2 syl ensymg mpd breq1 anbi2d biimprcd entr syl2an
      syl6 carden2b adantl bitrd ) ADEZBDEZPZAFGZBFGZHZAIGZBIGZHZABJKZVJVMVPVMV
      KCUACUBZUCUDUEUFUGUHUIUJZUKZGZVLVTGZHVJVPVKVLVTLVHVIWAVNWBVOCAVSVSULZMCBV
      SWCMNOVPVNVSGZVOVSGZHVJVMVNVOVSLVHVIWDVKWEVLCAVSWCQCBVSWCQNORVJVPVQVHAVNJ
      KZVOBJKZVPVQSVIVHVNAJKZWFVHVRAJKCTUMWHCAUNCAUOUPVNADUQURVIVRBJKCTUMWGCBUN
      CBUOUPWFWGPZVPWFVNBJKZPZVQVPWKWIVPWJWGWFVNVOBJUSUTVAAVNBVBVDVCVIVQVPSVHAB
      DVEVFRVG $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  $( Two possibly-empty 1-based finite sets of sequential integers are equal
     iff their endpoints are equal.  (Contributed by Paul Chapman,
     22-Jun-2011.)  (The proof was shortened by Mario Carneiro,
     29-Mar-2014.) $)
  fz1eqb $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
               ( ( 1 ... M ) = ( 1 ... N ) <-> M = N ) ) $=
    ( cn0 wcel wa c1 cfz co wceq chash cfv fveq2 hashfz1 eqeqan12d syl5ib oveq2
    impbid1 ) ACDZBCDZEZFAGHZFBGHZIZABIZUCUAJKZUBJKZITUDUAUBJLRSUEAUFBAMBMNOABF
    GPQ $.
    $( [29-Mar-2014] $) $( [22-Jun-2011] $)

  ${
    $d x A $.
    $( The size function of the cardinality function. $)
    hashcard $p |- ( A e. Fin -> ( # ` ( card ` A ) ) = ( # ` A ) ) $=
      ( vx cfn wcel ccrd cfv cvv cv c1 caddc co cmpt cc0 crdg com chash cardidm
      cres wceq hashgval fveq2i a1i wss ficardom ssid sylancl eqid syl 3eqtr3d
      ssnnfi ) ACDZAEFZEFZBGBHIJKLMNORZFZULUNFZULPFZAPFUOUPSUKUMULUNAQUAUBUKULC
      DZUOUQSUKULODULULUCURAUDULUEULULUJUFBULUNUNUGZTUHBAUNUSTUI $.
      $( [4-Nov-2013] $) $( [19-Sep-2013] $)
    $( Closure of the ` # ` function.  (Contributed by Paul Chapman,
       26-Oct-2012.)  (Revised by Mario Carneiro, 13-Jul-2014.) $)
    hashcl $p |- ( A e. Fin -> ( # ` A ) e. NN0 ) $=
      ( vx cfn wcel ccrd cfv cvv cv c1 caddc co cmpt cc0 crdg com cres cn0 eqid
      chash hashgval ficardom wf1o wf hashgf1o f1of ax-mp ffvelrni syl eqeltrrd
      ) ACDZAEFZBGBHIJKLMNOPZFZASFQBAULULRZTUJUKODUMQDAUAOQUKULOQULUBOQULUCBULU
      NUDOQULUEUFUGUHUI $.
      $( [13-Jul-2014] $) $( [26-Oct-2012] $)
  $}

  $( Two ways of saying a finite set is empty.  (Contributed by Paul Chapman,
     26-Oct-2012.)  (Revised by Mario Carneiro, 27-Jul-2014.) $)
  hasheq0 $p |- ( A e. V -> ( ( # ` A ) = 0 <-> A = (/) ) ) $=
    ( wcel cfn chash cfv wceq c0 wb wn wa cdif cr cpnf syl6eqel 0fin 0nn0 ax-mp
    cc0 id eldif wnel pnfnre df-nel mpbi hashinf eleq1d mtbiri 0re sylbir con3i
    nsyl adantl 2falsed ex c1 cfz co eqid cn0 fz1n fveq2i hashfz1 eqtr3i eqeq2i
    mpbir cen wbr hashen mpan2 en0 syl6bb syl5bbr pm2.61d2 ) ABCZADCZAEFZSGZAHG
    ZIZVOVPJZVTVOWAKZVRVSWBABDLCZVRJABDUAWCVQMCZVRWCWDNMCZNMUBWEJUCNMUDUEWCVQNM
    ABUFUGUHVRVQSMVRTUIOULUJWAVSJVOVSVPVSAHDVSTPOUKUMUNUOVRVQHEFZGZVPVSWFSVQUPS
    UQURZEFZWFSWHHEWHHGZSSGZSUSSUTCZWJWKIQSVARVFVBWLWISGQSVCRVDVEVPWGAHVGVHZVSV
    PHDCWGWMIPAHVIVJAVKVLVMVN $.
    $( [27-Jul-2014] $) $( [26-Oct-2012] $)

  $( The empty set has size zero.  (Contributed by Mario Carneiro,
     8-Jul-2014.) $)
  hash0 $p |- ( # ` (/) ) = 0 $=
    ( c0 chash cfv cc0 wceq eqid cvv wcel wb 0ex hasheq0 ax-mp mpbir ) ABCDEZAA
    EZAFAGHNOIJAGKLM $.
    $( [8-Jul-2014] $)

  $( The size of a singleton.  (Contributed by Paul Chapman, 26-Oct-2012.)
     (The proof was shortened by Mario Carneiro, 13-Feb-2013.) $)
  hashsng $p |- ( A e. V -> ( # ` { A } ) = 1 ) $=
    ( wcel csn chash cfv c1 cen wbr wceq cz 1z en2sn mpan2 wb snfi hashen mp2an
    cfn ax-mp sylibr cfz co cn0 1nn0 hashfz1 fzsn fveq2d syl5reqr syl6eq ) ABCZ
    ADZEFZGDZEFZGUKULUNHIZUMUOJZUKGKCZUPLAGBKMNULSCUNSCUQUPOAPGPULUNQRUAURUOGJL
    URGGGUBUCZEFZUOGUDCUTGJUEGUFTURUSUNEGUGUHUITUJ $.
    $( [13-Feb-2013] $) $( [26-Oct-2012] $)

  ${
    $d F f $.  $d N f $.
    $( The value of the size function on a finite 1-based sequence.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    fseq1hash $p |- ( ( N e. NN0 /\ F Fn ( 1 ... N ) ) -> ( # ` F ) = N ) $=
      ( cn0 wcel c1 cfz co wfn chash cfv wceq cen wbr cfn simpr sylancl syl2anc
      wa wb cvv fzfi fndmeng fnex a1i enfi hashen sylancr mpbird hashfz1 adantr
      mpbii eqtr3d ) BCDZAEBFGZHZRZUNIJZAIJZBUPUQURKZUNALMZUPUOUNNDZUTUMUOOZEBU
      AZUNNAUBZPUPVAANDZUSUTSVCUPVAVEVCUPATDZUTVAVESUPUOVAVFVBVCUNNAUCPUPUOVAUT
      VBVAUPVCUDVDQUNATUEQUKUNAUFUGUHUMUQBKUOBUIUJUL $.
      $( [27-Jul-2014] $) $( [26-Oct-2012] $)
  $}

  ${
    $d A n z $.  $d B n $.  $d G n z $.
    hashgadd.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    $( ` G ` maps ordinal addition to integer addition.  (Contributed by Paul
       Chapman, 30-Nov-2012.) $)
    hashgadd $p |- ( ( A e. om /\ B e. om ) ->
                     ( G ` ( A +o B ) ) = ( ( G ` A ) + ( G ` B ) ) ) $=
      ( com wcel coa co cfv caddc wceq wi c0 fveq2d oveq2d cc0 cn0 syl c1 vn vz
      cv csuc oveq2 fveq2 eqeq12d cc wf1o wf hashgf1o f1of ax-mp ffvelrni nn0cn
      imbi2d addid1 0z om2uz0i oveq2i a1i con0 oa0 3eqtr4rd w3a wa oasuc syl2an
      nnon nnacl om2uzsuci eqtrd 3adant3 ax-1cn addass mp3an3 3ad2ant3 3ad2ant2
      oveq1 3eqtr4d 3expia expcom a2d finds impcom ) CFGBFGZBCHIZDJZBDJZCDJZKIZ
      LZWFBUAUCZHIZDJZWIWMDJZKIZLZMWFBNHIZDJZWINDJZKIZLZMWFBUBUCZHIZDJZWIXDDJZK
      IZLZMWFBXDUDZHIZDJZWIXJDJZKIZLZMWFWLMUAUBCWMNLZWRXCWFXPWOWTWQXBXPWNWSDWMN
      BHUEOXPWPXAWIKWMNDUFPUGUPWMXDLZWRXIWFXQWOXFWQXHXQWNXEDWMXDBHUEOXQWPXGWIKW
      MXDDUFPUGUPWMXJLZWRXOWFXRWOXLWQXNXRWNXKDWMXJBHUEOXRWPXMWIKWMXJDUFPUGUPWMC
      LZWRWLWFXSWOWHWQWKXSWNWGDWMCBHUEOXSWPWJWIKWMCDUFPUGUPWFWIQKIZWIXBWTWFWIUH
      GZXTWILWFWIRGYAFRBDFRDUIFRDUJADEUKFRDULUMZUNWIUOSZWIUQSXBXTLWFXAQWIKAQDUR
      EUSUTVAWFWSBDWFBVBGZWSBLBVIZBVCSOVDXDFGZWFXIXOWFYFXIXOMWFYFXIXOWFYFXIVEZX
      LXFTKIZXNWFYFXLYHLXIWFYFVFZXLXEUDZDJZYHYIXKYJDWFYDXDVBGXKYJLYFYEXDVIBXDVG
      VHOYIXEFGYKYHLBXDVJAXEQDUREVKSVLVMYGXHTKIZWIXGTKIZKIZYHXNWFYFYLYNLZXIWFYA
      XGUHGZYOYFYCYFXGRGYPFRXDDYBUNXGUOSYAYPTUHGYOVNWIXGTVOVPVHVMXIWFYHYLLYFXFX
      HTKVSVQYFWFXNYNLXIYFXMYMWIKAXDQDUREVKPVRVTVLWAWBWCWDWE $.
      $( [15-Sep-2013] $) $( [30-Nov-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Dominance relation for the size function. $)
    hashdom $p |- ( ( A e. Fin /\ B e. Fin )
         -> ( ( # ` A ) <_ ( # ` B ) <-> A ~<_ B ) ) $=
      ( vy vx cfn wcel wa ccrd cfv coa co wceq com wbr chash cle ficardom caddc
      cn0 syl wss cv wrex cdom wb nnawordex syl2an ficarddom c1 cmin fzfi ax-mp
      cfz cvv cmpt cc0 crdg cres ccnv hashgval ad2antrr hashcl ad2antlr nn0sub2
      eqid simpr syl3anc hashfz1 syl5eq oveq12d cc nn0cn pncan3 adantr hashgadd
      eqtrd sylancl 3eqtr4d wf1o hashgf1o nnacl f1ocnvfv1 sylancr 3eqtr3d oveq2
      fveq2d eqeq1d rcla4ev ex cardnn adantl oveq2d fveq2 ssid ssnnfi oveqan12d
      wi mpan2 adantlr eqeq12d nn0ge0 nn0re mpbid breq2 syl5ibcom sylbid sylan2
      cr addge01 syl5 sylbird rexlimdva impbid 3bitr4rd ) AEFZBEFZGZAHIZBHIZUAZ
      XRCUBZJKZXSLZCMUCZABUDNAOIZBOIZPNZXOXRMFZXSMFZXTYDUEXPAQZBQZCXRXSUFUGABUH
      XQYGYDXQYGYDXQYGGZUIYFYEUJKZUMKZHIZMFZXRYOJKZXSLZYDYNEFZYPUIYMUKZYNQULZYL
      YQDUNDUBUIRKUOUPUQMURZIZUUBUSZIZXSUUBIZUUDIZYQXSYLUUCUUFUUDYLXRUUBIZYOUUB
      IZRKZYFUUCUUFYLUUJYEYMRKZYFYLUUHYEUUIYMRXOUUHYELXPYGDAUUBUUBVEZUTZVAYLUUI
      YNOIZYMYSUUIUUNLYTDYNUUBUULUTULYLYMSFZUUNYMLYLYESFZYFSFZYGUUOXOUUPXPYGAVB
      ZVAXPUUQXOYGBVBZVCXQYGVFYEYFVDVGYMVHTVIVJXQUUKYFLZYGXOYEVKFZYFVKFZUUTXPXO
      UUPUVAUURYEVLTXPUUQUVBUUSYFVLTYEYFVMUGVNVPYLYHYPUUCUUJLXOYHXPYGYJVAZUUADX
      RYOUUBUULVOVQXPUUFYFLZXOYGDBUUBUULUTZVCVRWFYLMSUUBVSZYQMFZUUEYQLDUUBUULVT
      ZYLYHYPUVGUVCUUAXRYOWAVQMSYQUUBWBWCYLUVFYIUUGXSLUVHXPYIXOYGYKVCMSXSUUBWBW
      CWDYCYRCYOMYAYOLYBYQXSYAYOXRJWEWGWHWCWIXQYCYGCMXQYAMFZGZYCXRYAHIZJKZXSLZY
      GUVJUVLYBXSUVJUVKYAXRJUVIUVKYALXQYAWJWKWLWGUVMUVLUUBIZUUFLZUVJYGUVLXSUUBW
      MUVIXQYAEFZUVOYGWQUVIYAYAUAUVPYAWNYAYAWOWRXQUVPGZUVOYEYAOIZRKZYFLZYGUVQUV
      NUVSUUFYFXOUVPUVNUVSLXPXOUVPGZUVNUUHUVKUUBIZRKZUVSXOYHUVKMFUVNUWCLUVPYJYA
      QDXRUVKUUBUULVOUGXOUVPUUHYEUWBUVRRUUMDYAUUBUULUTWPVPWSXPUVDXOUVPUVEVCWTUV
      QYEUVSPNZUVTYGXOUVPUWDXPUWAUPUVRPNZUWDUVPUWEXOUVPUVRSFZUWEYAVBZUVRXATWKXO
      YEXHFZUVRXHFZUWEUWDUEUVPXOUUPUWHUURYEXBTUVPUWFUWIUWGUVRXBTYEUVRXIUGXCWSUV
      SYFYEPXDXEXFXGXJXKXLXMXN $.
      $( [14-Feb-2014] $) $( [22-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The size of the union of disjoint finite sets is the sum of their
       sizes.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    hashun $p |- ( ( A e. Fin /\ B e. Fin /\ ( A i^i B ) = (/) ) ->
                   ( # ` ( A u. B ) ) = ( ( # ` A ) + ( # ` B ) ) ) $=
      ( vx cfn wcel cin c0 wceq w3a cun ccrd cfv cvv caddc co com chash 3adant3
      hashgval ficardom cv cmpt cc0 crdg cres coa ficardun fveq2d unfi eqid syl
      c1 wa hashgadd syl2an oveqan12d eqtrd 3eqtr3d ) ADEZBDEZABFGHZIZABJZKLZCM
      CUAULNOUBUCUDPUEZLZAKLZBKLZUFOZVELZVCQLZAQLZBQLZNOZVBVDVIVEABUGUHUSUTVFVK
      HZVAUSUTUMZVCDEVOABUICVCVEVEUJZSUKRUSUTVJVNHVAVPVJVGVELZVHVELZNOZVNUSVGPE
      VHPEVJVTHUTATBTCVGVHVEVQUNUOUSUTVRVLVSVMNCAVEVQSCBVEVQSUPUQRUR $.
      $( [15-Sep-2013] $) $( [30-Nov-2012] $)

    $( The size of the union of finite sets is less than or equal to the sum of
       their sizes.  (The proof was shortened by Mario Carneiro,
       27-Jul-2014.) $)
    hashun2 $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( # ` ( A u. B ) ) <_ ( ( # ` A ) + ( # ` B ) ) ) $=
      ( cfn wcel wa cun chash cfv cdif caddc co cle wceq wbr wb cr hashcl nn0re
      cn0 3syl undif2 fveq2i diffi c0 disjdif hashun mp3an3 sylan2 syl5eqr cdom
      cin adantl difss ssdomg simpr hashdom syl2anc mpbird simpl leadd2 syl3anc
      wss ee10 mpbid eqbrtrd ) ACDZBCDZEZABFZGHZAGHZBAIZGHZJKZVKBGHZJKZLVHVJAVL
      FZGHZVNVQVIGABUAUBVGVFVLCDZVRVNMZBAUCZVFVSAVLUKUDMVTABUEAVLUFUGUHUIVHVMVO
      LNZVNVPLNZVHWBVLBUJNZVHVSVLBVBWDVGVSVFWAULZBAUMVLBCUNVCVHVSVGWBWDOWEVFVGU
      OZVLBUPUQURVHVMPDZVOPDZVKPDZWBWCOVHVSVMSDWGWEVLQVMRTVHVGVOSDWHWFBQVORTVHV
      FVKSDWIVFVGUSAQVKRTVMVOVKUTVAVDVE $.
      $( [27-Jul-2014] $) $( [23-Sep-2013] $)
  $}

  $( The size of the union of a finite set with a disjoint singleton is one
     more than the size of the set.  (Contributed by Paul Chapman,
     30-Nov-2012.) $)
  hashunsng $p |- ( B e. V -> ( ( A e. Fin /\ -. B e. A ) ->
                    ( # ` ( A u. { B } ) ) = ( ( # ` A ) + 1 ) ) ) $=
    ( cfn wcel wn wa csn cun chash cfv c1 caddc co wceq cin disjsn snfi hashun
    c0 mp3an2 sylan2br hashsng oveq2d sylan9eq expcom ) ADEZBAEFZGZBCEZABHZIJKZ
    AJKZLMNZOUIUJULUMUKJKZMNZUNUHUGAUKPTOZULUPOZABQUGUKDEUQURBRAUKSUAUBUJUOLUMM
    BCUCUDUEUF $.
    $( [30-Nov-2012] $)

  ${
    hashpr.1 $e |- A e. _V $.
    hashpr.2 $e |- B e. _V $.
    $( The size of an unordered pair. $)
    hashpr $p |- ( A =/= B <-> ( # ` { A , B } ) = 2 ) $=
      ( wne cpr chash cfv c2 wceq csn cun c1 caddc co cfn wcel wn cvv ax-mp a1i
      snfi elsni eqcomd necon3ai wa hashunsng sylancr df-pr fveq2i df-2 hashsng
      wi oveq1i eqtr4i 3eqtr4g dfsn2 syl5req fveq2d syl6eq 1ne2 eqnetrd necon2i
      preq2 impbii ) ABEZABFZGHZIJVFAKZBKLZGHZVIGHZMNOZVHIVFVIPQZBVIQZRZVKVMJZA
      UBVOABVOBABAUCUDUEBSQVNVPUFVQUMDVIBSUGTUHVGVJGABUIUJIMMNOVMUKVLMMNASQVLMJ
      CASULTZUNUOUPABVHIABJZVHMIVSVHVLMVSVGVIGVSVIAAFVGAUQABAVDURUSVRUTMIEVSVAU
      AVBVCVE $.
      $( [27-Sep-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    hashxplem.1 $e |- B e. Fin $.
    $( Lemma for ~ hashxp . $)
    hashxplem $p |- ( A e. Fin ->
                      ( # ` ( A X. B ) ) = ( ( # ` A ) x. ( # ` B ) ) ) $=
      ( cxp chash cfv cmul co wceq c0 xpeq1 fveq2d fveq2 oveq1d eqeq12d cc0 cfn
      wcel ax-mp caddc vx vy vz cv csn cun cn0 cc hashcl nn0cn mul02 3syl hash0
      oveq1i xp0r fveq2i eqtri 3eqtr4ri wn oveq1 adantl xpundir xpfi mpan2 inxp
      wa cin disjsn biimpri syl syl6eq syl5eq snfi hashun mp3an2 syl2an cen wbr
      mp2an elexi xpcomen vex xpsnen entri wb hashen mpbir oveq2i adantr c1 cvv
      snex wi hashunsng ax-1cn adddir mp3an23 mulid2 eqtrd 3eqtr4d findcard2s
      ex ) UAUDZBDZEFZXCEFZBEFZGHZIJBDZEFZJEFZXGGHZIUBUDZBDZEFZXMEFZXGGHZIZXMUC
      UDZUEZUFZBDZEFZYAEFZXGGHZIZABDZEFZAEFZXGGHZIUAUBUCAXCJIZXEXJXHXLYKXDXIEXC
      JBKLYKXFXKXGGXCJEMNOXCXMIZXEXOXHXQYLXDXNEXCXMBKLYLXFXPXGGXCXMEMNOXCYAIZXE
      YCXHYEYMXDYBEXCYABKLYMXFYDXGGXCYAEMNOXCAIZXEYHXHYJYNXDYGEXCABKLYNXFYIXGGX
      CAEMNOPXGGHZPXLXJBQRZYOPIZCYPXGUGRZXGUHRZYQBUIZXGUJZXGUKULSXKPXGGUMUNXJXK
      PXIJEBUOUPUMUQURXMQRZXSXMRUSZVFZXRYFUUDXRVFXOXGTHZXQXGTHZYCYEXRUUEUUFIUUD
      XOXQXGTUTVAUUDYCUUEIXRUUDYCXNXTBDZUFZEFZUUEYBUUHEXMXTBVBUPUUDUUIXOUUGEFZT
      HZUUEUUBXNQRZXNUUGVGZJIZUUIUUKIZUUCUUBYPUULCXMBVCVDUUCUUMXMXTVGZBBVGZDZJX
      MBXTBVEUUCUURJUUQDZJUUCUUPJIZUURUUSIUUTUUCXMXSVHVIUUPJUUQKVJUUQUOVKVLUULU
      UGQRZUUNUUOXTQRYPUVAXSVMCXTBVCVSZXNUUGVNVOVPUUJXGXOTUUJXGIZUUGBVQVRZUUGBX
      TDBXTBXSWLBQCVTZWABXSUVEUCWBZWCWDUVAYPUVCUVDWEUVBCUUGBWFVSWGWHVKVLWIUUDYE
      UUFIXRUUDYEXPWJTHZXGGHZUUFUUDYDUVGXGGXSWKRUUDYDUVGIWMUVFXMXSWKWNSNUUBUVHU
      UFIUUCUUBUVHXQWJXGGHZTHZUUFUUBXPUGRXPUHRZUVHUVJIZXMUIXPUJUVKWJUHRYSUVLWOY
      PYSCYPYRYSYTUUAVJSZXPWJXGWPWQULUVIXGXQTYSUVIXGIUVMXGWRSWHVKWIWSWIWTXBXA
      $.
      $( [30-Nov-2012] $)
  $}

  $( The size of the Cartesian product of two finite sets is the product of
     their sizes.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
  hashxp $p |- ( ( A e. Fin /\ B e. Fin ) ->
                 ( # ` ( A X. B ) ) = ( ( # ` A ) x. ( # ` B ) ) ) $=
    ( cfn wcel cxp chash cfv cmul co wceq wi c0 cif xpeq2 fveq2d oveq2d eqeq12d
    fveq2 imbi2d 0fin elimel hashxplem dedth impcom ) BCDZACDZABEZFGZAFGZBFGZHI
    ZJZUEUFULKUFAUEBLMZEZFGZUIUMFGZHIZJZKBLBUMJZULURUFUSUHUOUKUQUSUGUNFBUMANOUS
    UJUPUIHBUMFRPQSAUMBLCTUAUBUCUD $.
    $( [30-Nov-2012] $)

  ${
    $d F a b $.  $d S a b $.  $d Y a b w $.  $d Z a b w $.
    hashpwlem.1 $e |- F = ( w e. ~P Y |-> ( w u. { Z } ) ) $.
    hashpwlem.2 $e |- S = { w | ( w \ Y ) = { Z } } $.
    $( Lemma for ~ hashpw . $)
    hashpwlem $p |- ( -. Z e. Y -> F : ~P Y -1-1-onto-> S ) $=
      ( va vb wcel wceq wral cun unex wa cdif c0 wss biimpi cin wn crn cfv wf1o
      cv wi cpw wfo wrex wfn csn vex snex cmpt copab df-mpt eqtri fnopab2 uneq1
      wf a1i fvmpt adantl difundir elpw ssdif0 bitri disjsn incom eqeq1i bitr3i
      disj3 eqcomd adantr uneq12d uncom un0 syl6eq syl5eq difeq1 eqeq1d eqeltrd
      elab2 sylibr ralrimiva ffnfv inss2 dfin4 difeq2 syl5req sseq1d mpbiri cvv
      sylanbrc difexg ax-mp syl undif1 difss sseq1 mpbii ssequn1 syl5eqr eqtr2d
      sylbi fveq2 eqeq2d rcla4ev rgen dffo3 forn eqeqan12d anbi12i ssel con3rr3
      jca wb syl6ibr anim12d eqtr3 syl6 imp sylan2b unineq sylan2 expcom sylbid
      ralrimivva w3a dff1o6 biimpri mp3an1 syl2anc ) EDJZUAZCUBBKZHUEZCUCZIUEZC
      UCZKZYQYSKZUFZIDUGZLHUUDLZUUDBCUDZYOUUDBCUHZYPYOUUDBCUTZYQYTKZIUUDUIZHBLZ
      UUGYOCUUDUJZYRBJZHUUDLUUHUULYOAHUUDAUEZEUKZMZCUUNUUOAULEUMZNCAUUDUUPUNUUN
      UUDJYQUUPKOAHUOFAHUUDUUPUPUQURZVAYOUUMHUUDYOYQUUDJZOZYRYQUUOMZBUUSYRUVAKY
      OAYQUUPUVAUUDCUUNYQUUOUSFYQUUOHULZUUQNZVBZVCUUTUVADPZUUOKZUVABJUUTUVEYQDP
      ZUUODPZMZUUOYQUUODVDUUTUVIQUUOMZUUOUUTUVGQUVHUUOUUSUVGQKZYOUUSUVKUUSYQDRZ
      UVKYQDUVBVEZYQDVFVGSVCYOUVHUUOKUUSYOUUOUVHYOUUOUVHKZYOUUODTZQKZUVNYODUUOT
      ZQKUVPDEVHUVQUVOQDUUOVIVJVKUUODVLVGSVMVNVOUVJUUOQMUUOQUUOVPUUOVQUQVRVSUUN
      DPZUUOKZUVFAUVABUVCUUNUVAKUVRUVEUUOUUNUVADVTWAGWCWDWBWEHUUDBCWFWNUUKYOUUJ
      HBYQBJZYQUUOPZUUDJZYQUWACUCZKZOZUUJUVTUVGUUOKZUWEUVSUWFAYQBUVBUUNYQKUVRUV
      GUUOUUNYQDVTWAGWCUWFUWBUWDUWFUWADRZUWBUWFUWGYQDTZDRYQDWGUWFUWAUWHDUWFUWHY
      QUVGPUWAYQDWHUVGUUOYQWIWJWKWLUWADYQWMJUWAWMJUVBYQUUOWMWOWPZVEWDZUWFUWCUWA
      UUOMZYQUWFUWBUWCUWKKUWJAUWAUUPUWKUUDCUUNUWAUUOUSFUWAUUOUWIUUQNVBWQUWFUWKU
      VAYQYQUUOWRUWFUUOYQRZUVAYQKUWFUVGYQRUWLYQDWSUVGUUOYQWTXAUWLUVAUUOYQMZYQUU
      OYQVPUWLUWMYQKUUOYQXBSXCWQVSXDXPXEUUIUWDIUWAUUDYSUWAKYTUWCYQYSUWACXFXGXHW
      QXIVAIHUUDBCXJWNUUDBCXKWQYOUUCHIUUDUUDYOUUSYSUUDJZOZOZUUAUVAYSUUOMZKZUUBU
      WOUUAUWRXQYOUUSUWNYRUVAYTUWQUVDAYSUUPUWQUUDCUUNYSUUOUSFYSUUOIULZUUQNVBXLV
      CUWRUWPUUBUWPUWRYQUUOTZYSUUOTZKZUUBUWOYOUVLYSDRZOZUXBUUSUVLUWNUXCUVMYSDUW
      SVEXMYOUXDUXBYOUXDUWTQKZUXAQKZOUXBYOUVLUXEUXCUXFYOUVLEYQJZUAUXEUVLUXGYNYQ
      DEXNXOYQEVHXRYOUXCEYSJZUAUXFUXCUXHYNYSDEXNXOYSEVHXRXSUWTUXAQXTYAYBYCUWRUX
      BOUUBYQYSUUOYDSYEYFYGYHUULYPUUEUUFUURUUFUULYPUUEYIHIUUDBCYJYKYLYM $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d A x y z $.  $d w x y z $.
    $( The size of the power set of a finite set is 2 raised to the power of
       the size of the set.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    hashpw $p |- ( A e. Fin -> ( # ` ~P A ) = ( 2 ^ ( # ` A ) ) ) $=
      ( vx vw cv cpw chash cfv c2 cexp co wceq c0 cun pweq oveq2d c1 cfn adantr
      wcel cab vy vz csn fveq2d fveq2 eqeq12d cc0 cc 2cn ax-mp hash0 oveq2i pw0
      exp0 fveq2i 0fin hashsng eqtri 3eqtr4ri wn wa cmul oveq1 adantl caddc cen
      cdif wbr cmpt wf1o eqid hashpwlem vex pwex f1oen syl pwfi biimpi crn cdom
      cvv wfn snex unex df-mpt fnopab2 fnrndomg mp2 domfi mpan2 f1ofo forn 3syl
      wb wfo eleq1d mpbid hashen syl2anc mpbird cn0 hashcl nn0cn times2 ssundif
      wss wo sssn bitri abbii unab 3eqtr4i ssdif0 uneq1i eqtr4i cin ineq1i inab
      df-pw snprc con1bii mpbir eqeq1 mtbiri con2i imnan mpbi abf hashun mp3an3
      wi syl5eq 3eqtr4rd hashunsng expp1 mpan eqtrd 3eqtr4d ex findcard2s ) BDZ
      EZFGZHUUAFGZIJZKLEZFGZHLFGZIJZKUADZEZFGZHUUJFGZIJZKZUUJUBDZUCZMZEZFGZHUUR
      FGZIJZKZAEZFGZHAFGZIJZKBUAUBAUUALKZUUCUUGUUEUUIUVHUUBUUFFUUALNUDUVHUUDUUH
      HIUUALFUEOUFUUAUUJKZUUCUULUUEUUNUVIUUBUUKFUUAUUJNUDUVIUUDUUMHIUUAUUJFUEOU
      FUUAUURKZUUCUUTUUEUVBUVJUUBUUSFUUAUURNUDUVJUUDUVAHIUUAUURFUEOUFUUAAKZUUCU
      VEUUEUVGUVKUUBUVDFUUAANUDUVKUUDUVFHIUUAAFUEOUFHUGIJZPUUIUUGHUHSZUVLPKUIHU
      NUJUUHUGHIUKULUUGLUCZFGZPUUFUVNFUMUOLQSUVOPKUPLQUQUJURUSUUJQSZUUPUUJSUTZV
      AZUUOUVCUVRUUOVAUULHVBJZUUNHVBJZUUTUVBUUOUVSUVTKUVRUULUUNHVBVCVDUVRUUTUVS
      KUUOUVRUULUULVEJZUULCDZUUJVGZUUQKZCTZFGZVEJZUVSUUTUVRUULUWFUULVEUVRUULUWF
      KZUUKUWEVFVHZUVQUWIUVPUVQUUKUWECUUKUWBUUQMZVIZVJZUWICUWEUWKUUJUUPUWKVKUWE
      VKVLZUUKUWEUWKUUJUAVMVNZVOVPVDUVRUUKQSZUWEQSZUWHUWIWNUVPUWOUVQUVPUWOUUJVQ
      VRZRZUVRUWKVSZQSZUWPUVPUWTUVQUVPUWOUWTUWQUWOUWSUUKVTVHZUWTUUKWASUWKUUKWBU
      XAUWNCBUUKUWJUWKUWBUUQCVMUUPWCWDCBUUKUWJWEWFUUKWAUWKWGWHUUKUWSWIWJVPRUVQU
      WTUWPWNUVPUVQUWSUWEQUVQUWLUUKUWEUWKWOUWSUWEKUWMUUKUWEUWKWKUUKUWEUWKWLWMWP
      VDWQZUUKUWEWRWSWTOUVRUWOUVSUWAKZUWRUWOUULXASUULUHSUXCUUKXBUULXCUULXDWMVPU
      VRUUTUUKUWEMZFGZUWGUUSUXDFUUSUWCLKZCTZUWEMZUXDUWBUURXFZCTUXFUWDXGZCTUUSUX
      HUXIUXJCUXIUWCUUQXFUXJUWBUUJUUQXEUWCUUPXHXIXJCUURXSUXFUWDCXKXLUUKUXGUWEUU
      KUWBUUJXFZCTUXGCUUJXSUXKUXFCUWBUUJXMXJURZXNXOUOUVRUWOUWPUXEUWGKZUWRUXBUWO
      UWPUUKUWEXPZLKUXMUXNUXGUWEXPZLUUKUXGUWEUXLXQUXOUXFUWDVAZCTLUXFUWDCXRUXPCU
      XFUWDUTYKUXPUTUWDUXFUWDUXFUUQLKZUXQUTUUPWASZUBVMZUXRUXQUUPXTYAYBUWCUUQLYC
      YDYEUXFUWDYFYGYHURURUUKUWEYIYJWSYLYMRUVRUVBUVTKUUOUVRUVBHUUMPVEJZIJZUVTUV
      RUVAUXTHIUXRUVRUVAUXTKYKUXSUUJUUPWAYNUJOUVPUYAUVTKZUVQUVPUUMXASZUYBUUJXBU
      VMUYCUYBUIHUUMYOYPVPRYQRYRYSYT $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d x y z F $.
    $( A finite set is a function iff it is equinumerous to its domain. $)
    hashfun $p |- ( F e. Fin -> ( Fun F <-> ( # ` F ) = ( # ` dom F ) ) ) $=
      ( vx vy vz cfn wcel chash cfv cdm wceq wbr adantr cr clt syl c1 caddc cle
      c0 c2 wfun wa cen simpl fundmeng ensymg sylc wb dmfi hashen syldan mpbird
      wrel cv cop wi wal wn wne cn0 hashcl nn0re cvv cxp wrex wral df-rel dfss3
      wss bitri notbii rexnal bitr4i w3a csn cdif co dmun fveq2i dmsnn0 biimpri
      cun necon1bi 3ad2ant3 uneq2d un0 syl6eq fveq2d syl5eq diffi peano2re cdom
      fidomdm hashdom syl2anc ltp1 lelttrd 3ad2ant1 eqbrtrd incom disjdif eqtri
      cin hashun mp3an23 vex hashsng ax-mp oveq2i syl6req breqtrd difsnid dmeqd
      snfi 3ad2ant2 3brtr3d 3exp rexlimdv imp ltne syl3anc necon4bd wex 2nalexn
      syl5bi df-ne anbi2i annim exbii bitr2i 2exbii cpr 2re readdcl sylancr 1re
      ex exnal opex ad2antrl undif biimpi syl5reqr dmprop eqtr4i uneq1i hashun2
      prss dfsn2 oveq1i syl6breq 1lt2 ltadd1 mp3an12 mpbii leadd2 mpbid mp3an13
      mp3an3 prfi opth2 necon3i hashpr oveq1d ad2antll 3eqtr3rd ltletrd exlimdv
      sylib exlimdvv dffun4 sylanbrc impbida ) AEFZAUAZAGHZAIZGHZJZUVNUVOUBZUVS
      AUVQUCKZUVTUVNUVQAUCKUWAUVNUVOUDAEUEUVQAEUFUGUVNUVOUVQEFZUVSUWAUHUVNUWBUV
      OAUIZLAUVQUJUKULUVNUVSUBAUMZBUNZCUNZUOZAFUWEDUNZUOZAFUBZUWFUWHJZUPZDUQZCU
      QBUQZUVOUVNUVSUWDUVNUWDUVPUVRUVNUWDURZUVPUVRUSZUVNUWOUBUVRMFZUVPMFZUVRUVP
      NKZUWPUVNUWQUWOUVNUVRUTFZUWQUVNUWBUWTUWCUVQVAOUVRVBOZLUVNUWRUWOUVNUVPUTFU
      WRAVAUVPVBOZLUVNUWOUWSUWOUWEVCVCVDZFZURZBAVEZUVNUWSUWOUXDBAVFZURUXFUWDUXG
      UWDAUXCVIUXGAVGBAUXCVHVJVKUXDBAVLVMUVNUXEUWSBAUVNUWEAFZUXEUWSUVNUXHUXEVNZ
      AUWEVOZVPZUXJWBZIZGHZUXLGHZUVRUVPNUXIUXNUXKGHZPQVQZUXONUXIUXNUXKIZGHZUXQN
      UXIUXNUXRUXJIZWBZGHUXSUXMUYAGUXKUXJVRVSUXIUYAUXRGUXIUYAUXRSWBUXRUXIUXTSUX
      RUXEUVNUXTSJUXHUXDUXTSUXDUXTSUSUWEVTWAWCWDWEUXRWFWGWHWIUVNUXHUXSUXQNKUXEU
      VNUXSUXPUXQUVNUXSUTFZUXSMFUVNUXREFZUYBUVNUXKEFZUYCAUXJWJZUXKUIOZUXRVAOUXS
      VBOUVNUXPUTFZUXPMFZUVNUYDUYGUYEUXKVAOUXPVBOZUVNUYHUXQMFUYIUXPWKOUVNUXSUXP
      RKZUXRUXKWLKZUVNUYDUYKUYEUXKWMOUVNUYCUYDUYJUYKUHUYFUYEUXRUXKWNWOULUVNUYHU
      XPUXQNKUYIUXPWPOWQWRWSUVNUXHUXQUXOJUXEUVNUXOUXPUXJGHZQVQZUXQUVNUYDUXOUYMJ
      ZUYEUYDUXJEFZUXKUXJXCZSJUYNUWEXNZUYPUXJUXKXCSUXKUXJWTUXJAXAXBUXKUXJXDXEOU
      YLPUXPQUWEVCFUYLPJBXFUWEVCXGXHZXIXJWRXKUXHUVNUXNUVRJUXEUXHUXMUVQGUXHUXLAA
      UWEXLZXMWHXOUXHUVNUXOUVPJUXEUXHUXLAGUYSWHXOXPXQXRYEXSUVRUVPXTZYAYQYBXSUVN
      UVSUWNUVNUWNUVPUVRUWNURZUWJUWFUWHUSZUBZDYCZCYCBYCZUVNUWPVUAUWMURZCYCBYCVU
      EUWMBCYDVUFVUDBCVUDUWLURZDYCVUFVUCVUGDVUCUWJUWKURZUBVUGVUBVUHUWJUWFUWHYFY
      GUWJUWKYHVJYIUWLDYRYJYKVJUVNVUDUWPBCUVNVUCUWPDUVNVUCUWPUVNVUCUBZUWQUWRUWS
      UWPUVNUWQVUCUXALZUVNUWRVUCUXBLZVUIUVRTAUWGUWIYLZVPZIZGHZQVQZUVPVUJVUITMFZ
      VUOMFZVUPMFZYMUVNVURVUCUVNVUOUTFZVURUVNVUNEFZVUTUVNVUMEFZVVAAVULWJZVUMUIO
      ZVUNVAOVUOVBOZLTVUOYNZYOVUKVUIUVRPVUOQVQZVUPVUJUVNVVGMFZVUCUVNPMFZVURVVHY
      PVVEPVUOYNYOLUVNVUSVUCUVNVUQVURVUSYMVVEVVFYOLVUIUVRUXJVUNWBZGHZVVGRUWJUVR
      VVKJUVNVUBUWJUVQVVJGUWJUVQVULIZVUNWBZVVJUWJVVMVULVUMWBZIUVQVULVUMVRUWJVVN
      AUWJVVNAJZUWJVULAVIVVOUWGUWIAUWEUWFYSZUWEUWHYSZUUHVULAUUAVJUUBZXMUUCVVLUX
      JVUNVVLUWEUWEYLUXJUWEUWEUWFUWHUUDUWEUUIUUEUUFWGWHYTUVNVVKVVGRKVUCUVNVVKUY
      LVUOQVQZVVGRUVNUYOVVAVVKVVSRKUYQVVDUXJVUNUUGYOUYLPVUOQUYRUUJUUKLWSUVNVVGV
      UPNKZVUCUVNPTNKZVVTUULUVNVURVWAVVTUHZVVEVVIVUQVURVWBYPYMPTVUOUUMUUNOUUOLW
      QVUIVUPTVUMGHZQVQZUVPRUVNVUPVWDRKZVUCUVNVUOVWCRKZVWEUVNVWFVUNVUMWLKZUVNVV
      BVWGVVCVUMWMOUVNVVAVVBVWFVWGUHVVDVVCVUNVUMWNWOULUVNVURVWCMFZVWFVWEUHZVVEU
      VNVWCUTFZVWHUVNVVBVWJVVCVUMVAOVWCVBOVURVWHVUQVWIYMVUOVWCTUUPUUSWOUUQLVUIV
      VNGHZVULGHZVWCQVQZUVPVWDUVNVWKVWMJZVUCUVNVVBVWNVVCVULEFVVBVULVUMXCSJVWNUW
      GUWIUUTVULAXAVULVUMXDUUROLUWJVWKUVPJUVNVUBUWJVVNAGVVRWHYTVUBVWMVWDJUVNUWJ
      VUBVWLTVWCQVUBUWGUWIUSVWLTJUWGUWIUWFUWHUWEUWFUWEUWHCXFDXFUVAUVBUWGUWIVVPV
      VQUVCUVIUVDUVEUVFXKUVGUYTYAYQUVHUVJYEYBXSBCDAUVKUVLUVM $.
      $( [26-Sep-2013] $)
  $}

  ${
    $d j k u v w x y z A $.  $d j k u v x K $.  $d u v x ph $.
    ${
      hashbc.1 $e |- ( ph -> A e. Fin ) $.
      hashbc.2 $e |- ( ph -> -. z e. A ) $.
      hashbc.3 $e |- ( ph -> A. j e. ZZ
        ( ( # ` A ) _C j ) = ( # ` { x e. ~P A | ( # ` x ) = j } ) ) $.
      hashbc.4 $e |- ( ph -> K e. ZZ ) $.
      $( Lemma for ~ hashbc : inductive step. $)
      hashbclem $p |- ( ph -> ( ( # ` ( A u. { z } ) ) _C K ) =
        ( # ` { x e. ~P ( A u. { z } ) | ( # ` x ) = K } ) ) $=
        ( chash cfv co c1 caddc wcel wceq wa wss cfn vu vv cbc cmin csn cun cpw
        cv wn crab cz wral oveq2 eqeq2 rabbidv fveq2d eqeq12d rcla4v sylc ssun1
        cab sspwb mpbi sseli adantl wi elpwi con3d syl mpan9 jca uncom syl6sseq
        ssel adantr cin c0 wb simpr disjsn sylibr disjssun mpbid impbida anbi1d
        vex elpw anass syl6bb abbidv df-rab 3eqtr4g eqtrd peano2zm cen wbr cdif
        cvv pwfi sylib rabexg fveq2 eqeq1d elrab ad2antrl unss1 snex unex ssun2
        snss mpbir a1i ssfi syl2anc snfi hashun syl3anc ax-mp oveq12d cc ax-1cn
        sylancl eleq2 anbi12d sylanbrc ex syl5bi cn0 hashcl snssd syl5eq oveq2i
        incom eqtri syl6eq oveq1d eqcom bitri ssrab2 wo mtod simprr hashsng zcn
        sseld npcan 3eqtrd ssundif difexg nn0cn simprrl ssequn2 disjdif simprrr
        3syl undif1 3eqtr3d eqtr3d anbi12i w3a simp3rl 3adant3 uneqdifeq bicomd
        pncan eqeq1i 3bitr4g 3expib en3d unfi hashen mpbird bcpasc eqtr4d pm2.1
        biantrur andir rabbiia unrab eqtr4i fveq2i simprl simpll pm2.65i rabeq0
        inrab rgenw 3eqtr4d ) ADKLZFUCMZUWIFNUDMZUCMZOMZCUHZBUHZPZUIZUWOKLZFQZR
        ZBDUWNUEZUFZUGZUJZKLZUWPUWSRZBUXCUJZKLZOMZUXBKLZFUCMZUWSBUXCUJZKLZAUWJU
        XEUWLUXHOAUWJUWSBDUGZUJZKLZUXEAFUKPZUWIEUHZUCMZUWRUXRQZBUXNUJZKLZQZEUKU
        LZUWJUXPQZJIUYCUYEEFUKUXRFQZUXSUWJUYBUXPUXRFUWIUCUMUYFUYAUXOKUYFUXTUWSB
        UXNUXRFUWRUNUOUPUQURUSAUXOUXDKAUWOUXNPZUWSRZBVAUWOUXCPZUWTRZBVAUXOUXDAU
        YHUYJBAUYHUYIUWQRZUWSRUYJAUYGUYKUWSAUYGUYKAUYGRUYIUWQUYGUYIAUXNUXCUWODU
        XBSUXNUXCSDUXAUTDUXBVBVCVDVEAUWNDPZUIZUYGUWQHUYGUWODSZUYMUWQVFUWODVGUYN
        UWPUYLUWODUWNVNVHVIVJVKUYKUYGAUYKUYNUYGUYKUWOUXADUFZSZUYNUYIUYPUWQUYIUW
        OUXBUYOUWOUXBVGDUXAVLZVMVOUYKUWOUXAVPVQQZUYPUYNVRUYKUWQUYRUYIUWQVSUWOUW
        NVTWAUWOUXADWBVIWCUWODBWFWGWAVEWDWEUYIUWQUWSWHWIWJUWSBUXNWKUWTBUXCWKWLU
        PWMAUWLUWRUWKQZBUXNUJZKLZUXHAUWKUKPZUYDUWLVUAQZAUXQVUBJFWNVIIUYCVUCEUWK
        UKUXRUWKQZUXSUWLUYBVUAUXRUWKUWIUCUMVUDUYAUYTKVUDUXTUYSBUXNUXRUWKUWRUNUO
        UPUQURUSAVUAUXHQZUYTUXGWOWPZAUAUBUYTUXGUAUHZUXAUFZUBUHZUXAWQZAUXNTPZUYT
        WRPADTPZVUKGDWSWTZUYSBUXNTXAVIVUGUYTPZVUGUXNPZVUGKLZUWKQZRZAVUHUXGPZUYS
        VUQBVUGUXNUWOVUGQUWRVUPUWKUWOVUGKXBXCXDZAVURVUSAVURRZVUHUXCPZUWNVUHPZVU
        HKLZFQZRZVUSVVAVUHUXBSZVVBVVAVUGDSZVVGVUOVVHAVUQVUGDVGXEZVUGDUXAXFVIVUH
        UXBVUGUXAUAWFUWNXGXHWGWAVVAVVCVVEVVCVVAVVCUXAVUHSUXAVUGXIUWNVUHCWFZXJXK
        XLVVAVVDVUPUXAKLZOMZUWKNOMZFVVAVUGTPZUXATPZVUGUXAVPZVQQZVVDVVLQVVAVULVV
        HVVNAVULVURGVOVVIDVUGXMXNVVOVVAUWNXOZXLVVAUWNVUGPZUIVVQVVAVVSUYLAUYMVUR
        HVOVVAVUGDUWNVVIUUEUUAVUGUWNVTWAZVUGUXAXPXQVVAVUPUWKVVKNOAVUOVUQUUBVVKN
        QZVVAUWNWRPVWAVVJUWNWRUUCXRZXLXSVVAFXTPZNXTPZVVMFQVVAUXQVWCAUXQVURJVOFU
        UDVIYAFNUUFYBUUGVKUXFVVFBVUHUXCUWOVUHQZUWPVVCUWSVVEUWOVUHUWNYCVWEUWRVVD
        FUWOVUHKXBXCYDXDYEYFYGVUIUXGPZVUIUXCPZUWNVUIPZVUIKLZFQZRZRZAVUJUYTPZUXF
        VWKBVUIUXCUWOVUIQZUWPVWHUWSVWJUWOVUIUWNYCVWNUWRVWIFUWOVUIKXBXCYDXDZAVWL
        VWMAVWLRZVUJUXNPZVUJKLZUWKQZVWMVWPVUJDSZVWQVWPVUIUYOSVWTVWPVUIUXBUYOVWG
        VUIUXBSAVWKVUIUXBVGXEUYQVMVUIUXADUUHWTZVUJDVUIWRPVUJWRPUBWFVUIUXAWRUUIX
        RWGWAVWPVWRNOMZNUDMZVWRUWKVWPVWRXTPZVWDVXCVWRQVWPVUJTPZVWRYHPVXDVWPVULV
        WTVXEAVULVWLGVOVXADVUJXMXNZVUJYIVWRUUJUUOYAVWRNUVEYBVWPVXBFNUDVWPVUJUXA
        UFZKLZVWIVXBFVWPVXGVUIKVWPVXGVUIUXAUFZVUIVUIUXAUUPVWPUXAVUISZVXIVUIQVWP
        UWNVUIAVWGVWHVWJUUKYJUXAVUIUULWTYKUPVWPVXHVWRVVKOMZVXBVWPVXEVVOVUJUXAVP
        ZVQQZVXHVXKQVXFVVOVWPVVRXLVXMVWPVXLUXAVUJVPVQVUJUXAYMUXAVUIUUMYNXLVUJUX
        AXPXQVVKNVWROVWBYLYOAVWGVWHVWJUUNUUQYPUURUYSVWSBVUJUXNUWOVUJQUWRVWRUWKU
        WOVUJKXBXCXDYEYFYGVUNVWFRVURVWLRAVUGVUJQZVUIVUHQZVRZVUNVURVWFVWLVUTVWOU
        USAVURVWLVXPAVURVWLUUTZVUJVUGQZUXAVUGUFZVUIQZVXNVXOVXQVXTVXRVXQVXJUXAVU
        GVPZVQQVXTVXRVRVXQUWNVUIVWHVWJVWGAVURUVAYJVXQVYAVVPVQUXAVUGYMAVURVVQVWL
        VVTUVBYKUXAVUGVUIUVCXNUVDVUGVUJYQVXOVUHVUIQVXTVUIVUHYQVUHVXSVUIVUGUXAVL
        UVFYRUVGUVHYGUVIAUYTTPZUXGTPZVUEVUFVRAVUKUYTUXNSVYBVUMUYSBUXNYSUXNUYTXM
        YBAUXCTPZUXGUXCSVYCAUXBTPZVYDAVULVVOVYEGVVRDUXAUVJYBUXBWSWTZUXFBUXCYSUX
        CUXGXMYBZUYTUXGUVKXNUVLWMXSAUXKUWINOMZFUCMZUWMAUXJVYHFUCAUXJUWIVVKOMZVY
        HAVULVVODUXAVPVQQZUXJVYJQGVVOAVVRXLAUYMVYKHDUWNVTWADUXAXPXQVVKNUWIOVWBY
        LYOYPAUWIYHPZUXQUWMVYIQAVULVYLGDYIVIJFUWIUVMXNUVNAUXMUXDUXGUFZKLZUXIUXL
        VYMKUXLUWTUXFYTZBUXCUJVYMUWSVYOBUXCUWSVYOVRUYIUWSUWQUWPYTZUWSRVYOVYPUWS
        UWPUVOUVPUWQUWPUWSUVQYRXLUVRUWTUXFBUXCUVSUVTUWAAUXDTPZVYCUXDUXGVPZVQQZV
        YNUXIQAVYDUXDUXCSVYQVYFUWTBUXCYSUXCUXDXMYBVYGVYSAVYRUWTUXFRZBUXCUJZVQUW
        TUXFBUXCUWFWUAVQQVYTUIZBUXCULWUBBUXCVYTUWPUWTUWPUWSUWBUWQUWSUXFUWCUWDUW
        GVYTBUXCUWEXKYNXLUXDUXGXPXQYKUWH $.
        $( [13-Jul-2014] $)
    $}

    $( The binomial coefficient counts the number of subsets of a finite set of
       a given size.  (Contributed by Mario Carneiro, 13-Jul-2014.) $)
    hashbc $p |- ( ( A e. Fin /\ K e. ZZ ) ->
      ( ( # ` A ) _C K ) = ( # ` { x e. ~P A | ( # ` x ) = K } ) ) $=
      ( vk vj wcel chash cfv cv cbc co wceq crab cz c0 fveq2 fveq2d eqeq12d cc0
      wral vw vy vz cfn cpw csn cun oveq1d pweq rabeq syl ralbidv cfz 0nn0 bcn0
      c1 cn0 ax-mp hash0 a1i elfz1eq oveq12d pw0 eqcomd raleqi 0ex ralsn syl6eq
      wsbc eqeq1d sbcie 3bitri sylibr rabid2 syl5reqr hashsng 3eqtr4a adantl wn
      cvv wa oveq1i bcval3 mp3an1 id elfz3 syl6eqelr con3i notbid rabeq0 eqtr4d
      0z syl5eq pm2.61dan rgen oveq2 eqeq2 rabbidv cbvrabv simpll simplr simprr
      cbvralv fveq2i eqeq2i ralbii simprl hashbclem ralrimdva syl5bi findcard2s
      expr rcla4cva sylan ) BUDFBGHZDIZJKZAIZGHZXPLZABUEZMZGHZLZDNTZCNFXOCJKZXS
      CLZAYAMZGHZLZUAIZGHZXPJKZXTAYKUEZMZGHZLZDNTOGHZXPJKZXTAOUEZMZGHZLZDNTUBIZ
      GHZXPJKZXTAUUDUEZMZGHZLZDNTZUUDUCIZUFUGZGHZXPJKZXTAUUMUEZMZGHZLZDNTZYEUAU
      BUCBYKOLZYQUUCDNUVAYMYSYPUUBUVAYLYRXPJYKOGPUHUVAYOUUAGUVAYNYTLYOUUALYKOUI
      XTAYNYTUJUKQRULYKUUDLZYQUUJDNUVBYMUUFYPUUIUVBYLUUEXPJYKUUDGPUHUVBYOUUHGUV
      BYNUUGLYOUUHLYKUUDUIXTAYNUUGUJUKQRULYKUUMLZYQUUSDNUVCYMUUOYPUURUVCYLUUNXP
      JYKUUMGPUHUVCYOUUQGUVCYNUUPLYOUUQLYKUUMUIXTAYNUUPUJUKQRULYKBLZYQYDDNUVDYM
      XQYPYCUVDYLXOXPJYKBGPUHUVDYOYBGUVDYNYALYOYBLYKBUIXTAYNYAUJUKQRULUUCDNXPNF
      ZXPSSUMKZFZUUCUVGUUCUVEUVGSSJKZUPYSUUBSUQFZUVHUPLUNSUOURUVGYRSXPSJYRSLUVG
      USUTXPSVAZVBUVGUUBOUFZGHZUPUVGUUAUVKGUVGUVKYTUUAVCUVGXTAYTTZYTUUALUVGSXPL
      ZUVMUVGXPSUVJVDUVMXTAUVKTXTAOVIUVNXTAYTUVKVCVEXTAOVFVGXTUVNAOVFXROLZXSSXP
      UVOXSYRSXROGPUSVHVJZVKVLVMXTAYTVNVMVOQOVTFUVLUPLVFOVTVPURVHVQVRUVEUVGVSZW
      AZYSSXPJKZUUBYRSXPJUSWBUVRUVSSUUBUVIUVEUVQUVSSLUNXPSWCWDUVRUUBYRSUVRUUAOG
      UVRXTVSZAYTTZUUAOLUVRUVNVSZUWAUVQUWBUVEUVNUVGUVNXPSUVFUVNWESNFSUVFFWLSWFU
      RWGWHVRUWAUVTAUVKTUVTAOVIUWBUVTAYTUVKVCVEUVTAOVFVGUVTUWBAOVFUVOXTUVNUVPWI
      VKVLVMXTAYTWJVMQUSVHWKWMWNWOUUKUUEEIZJKZUULGHZUWCLZUCUUGMZGHZLZENTZUUDUDF
      ZUULUUDFVSZWAZUUTUUJUWIDENXPUWCLZUUFUWDUUIUWHXPUWCUUEJWPUWNUUHUWGGUWNUUHX
      SUWCLZAUUGMZUWGUWNXTUWOAUUGXPUWCXSWQWRUWOUWFAUCUUGXRUULLXSUWEUWCXRUULGPVJ
      WSZVHQRXCUWMUWJUUSDNUWMUVEUWJUUSUWMUVEUWJWAZWAZAUCUUDEXPUWKUWLUWRWTUWKUWL
      UWRXAUWSUWJUWDUWPGHZLZENTUWMUVEUWJXBUXAUWIENUWTUWHUWDUWPUWGGUWQXDXEXFVMUW
      MUVEUWJXGXHXLXIXJXKYDYJDCNXPCLZXQYFYCYIXPCXOJWPUXBYBYHGUXBXTYGAYAXPCXSWQW
      RQRXMXN $.
      $( [13-Jul-2014] $)
  $}

  ${
    $d f n x A $.  $d f B $.  $d f G $.  $d f n x R $.
    fz1iso.1 $e |- G = ( rec ( ( n e. _V |-> ( n + 1 ) ) , 1 ) |` om ) $.
    fz1iso.2 $e |- B = ( NN i^i ( `' < " { ( ( # ` A ) + 1 ) } ) ) $.
    fz1iso.3 $e |- C = ( om i^i ( `' G ` ( ( # ` A ) + 1 ) ) ) $.
    $( Lemma for ~ fz1iso . $)
    fz1isolem $p |- ( ( R Or A /\ A e. Fin ) ->
                  E. f f Isom < , R ( ( 1 ... ( # ` A ) ) , A ) ) $=
      ( wcel cep wiso con0 c1 cfv clt cn com cvv vx wor cfn wa cv wex chash cfz
      wrex co wreu wwe simpr wofi ordtype syl2anc reurex syl ccnv cres ccom cn0
      hashcl ad2antlr caddc cuz wceq nnuz om2uzisoi isoeq5 mpbiri ax-mp nn0p1nn
      1z isocnv cin csn cima fvex epini ineq2i eqtr4i isoini2 sylancr wb cle cz
      wbr nnz nn0z eluz syl2anr zleltp1 ovex vex eliniseg syl6bbr bitr2d eleq2i
      pm5.32da elin bitri elfzuzb anbi1i bitr4i 3bitr4g eqrdv isoeq4 mpbid cmpt
      cc0 crdg simprl simplr wf1o simprr isof1o f1oen 3syl enfi mpbird sylanbrc
      cen onfin2 syl6eleqr cmin eqid uzrdgxfr ax-1cn subid1i oveq2i syl6eq ccrd
      0z ensymg sylc cardennn fveq2d eqtr3d eqtrd hashgval oveq1d f1ocnvfv1 wss
      ineq2d word ordom ordelss sseqin2 sylib isotr wfun wfn rdgfnon fnfun omex
      syl5eq resfunexg mp2an eqeltri cnvex coex isoeq1 cla4ev exlimdv rexlimdva
      resex expr mpd ) ADUBZAUCKZUDZUAUEZALDFUEZMZFUFZUANUIZOAUGPZUHUJZAQDEUEZM
      ZEUFZUVLUVPUANUKZUVQUVLUVKADULUWCUVJUVKUMADUNUAAUCDFUOUPUVPUANUQURUVLUVPU
      WBUANUVLUVMNKZUDUVOUWBFUVLUWDUVOUWBUVLUWDUVOUDZUDZUVSAQDUVNGUSZBUTZVAZMZU
      WBUWFUVSUVMQLUWHMZUVOUWJUWFUVSCQLUWHMZUWKUWFBCQLUWHMZUWLUWFUVRVBKZUWMUVKU
      WNUVJUWEAVCVDZUWNRSQLUWGMZUVROVEUJZRKUWMSRLQGMZUWPROVFPZVGZUWRVHUWTUWRSUW
      SLQGMFOGVNHVISRUWSLQGVJVKVLZSRLQGVOVLUVRVMRSBCQLUWGUWQICSUWQUWGPZVPZSLUSU
      XBVQVRZVPJUXDUXBSUXBUWQUWGVSVTWAWBWCWDURUWFBUVSVGUWMUWLWEUWFEBUVSUWFUVTRK
      ZUVTQUSUWQVQVRZKZUDZUXEUVRUVTVFPKZUDZUVTBKZUVTUVSKZUWFUXEUXGUXIUWFUXEUDZU
      XIUVTUVRWFWHZUXGUXEUVTWGKZUVRWGKZUXIUXNWEUWFUVTWIZUWFUWNUXPUWOUVRWJURZUVT
      UVRWKWLUXMUXNUVTUWQQWHZUXGUXEUXOUXPUXNUXSWEUWFUXQUXRUVTUVRWMWLUWQTKUXGUXS
      WEUVROVEWNQUWQUVTTEWOWPVLWQWRWTUXKUVTRUXFVPZKUXHBUXTUVTIWSUVTRUXFXAXBUXLU
      VTUWSKZUXIUDZUXJUVRTKUXLUYBWEAUGVSTUVTOUVRXCVLUXEUYAUXIRUWSUVTVHWSXDXEXFX
      GBCUVSQLUWHXHURXIUWFCUVMVGUWLUWKWEUWFCUXCUVMJUWFUXCSUVMVPZUVMUWFUXBUVMSUW
      FUVMGPZUWGPZUXBUVMUWFUYDUWQUWGUWFUYDUVMFTUVNOVEUJXJZXKXLSUTZPZOVEUJZUWQUW
      FUVMSKZUYDUYIVGUWFUVMNUCVPZSUWFUWDUVMUCKZUVMUYKKUVLUWDUVOXMUWFUYLUVKUVJUV
      KUWEXNZUWFUVKUVMAYCWHZUYLUVKWEUYMUWFUVOUVMAUVNXOUYNUVLUWDUVOXPZUVMALDUVNX
      QUVMAUVNUAWOXRXSZUVMAUCXTUPYAUVMNUCXAYBYDYEZUYJUYDUYHOXKYFUJZVEUJUYIFOXKG
      UYGUVMHUYGYGZVNYNYHUYROUYHVEOYIYJYKYLURUWFUYHUVROVEUWFAYMPZUYGPZUYHUVRUWF
      UYTUVMUYGUWFAUVMYCWHZUYJUYTUVMVGUWFUVKUYNVUBUYMUYPUVMAUCYOYPUYQAUVMYQUPYR
      UVKVUAUVRVGUVJUWEFAUYGUYSUUAVDYSUUBYTYRUWFSRGXOZUYJUYEUVMVGUWRVUCUXASRLQG
      XQVLUYQSRUVMGUUCWDYSUUEUWFUVMSUUDZUYCUVMVGUWFSUUFUYJVUDUUGUYQSUVMUUHWDUVM
      SUUIUUJYTUUQUVSCUVMQLUWHVJURXIUYOUVSUVMAQLDUVNUWHUUKUPUWAUWJEUWIUVNUWHFWO
      UWGBGGUYFOXLZSUTZTHVUEUULZSTKVUFTKVUENUUMVUGOUYFUUNNVUEUUOVLUUPVUESTUURUU
      SUUTUVAUVGUVBUVSAQDUWIUVTUVCUVDURUVHUVEUVFUVI $.
      $( [2-Apr-2014] $)
  $}

  ${
    $d f n A $.  $d f n R $.
    $( Any finite ordered set has an order isometry to a one-based finite
       sequence.  (Contributed by Mario Carneiro, 2-Apr-2014.) $)
    fz1iso $p |- ( ( R Or A /\ A e. Fin ) ->
                  E. f f Isom < , R ( ( 1 ... ( # ` A ) ) , A ) ) $=
      ( vn cn clt ccnv chash cfv c1 caddc co csn cima cin com cvv cv cmpt eqid
      crdg cres fz1isolem ) AEFGAHIJKLZMNOZPUDDQDRJKLSJUAPUBZGIOZBCDUFUFTUETUGT
      UC $.
      $( [2-Apr-2014] $)
  $}

  ${
    $d k m n y A $.  $d k m n y F $.  $d k m n y G $.  $d m n y H $.  $d y N $.
    $d k m n y M $.  $d k m n y P $.  $d k m n y ph $.  $d k n S $.  $d k Z $.
    seqcoll.1 $e |- ( ( ph /\ k e. S ) -> ( Z P k ) = k ) $.
    seqcoll.1b $e |- ( ( ph /\ k e. S ) -> ( k P Z ) = k ) $.
    seqcoll.c $e |- ( ( ph /\ ( k e. S /\ n e. S ) ) -> ( k P n ) e. S ) $.
    seqcoll.a $e |- ( ph -> Z e. S ) $.
    seqcoll.2 $e |- ( ph -> G Isom < , < ( ( 1 ... ( # ` A ) ) , A ) ) $.
    seqcoll.3 $e |- ( ph -> N e. ( 1 ... ( # ` A ) ) ) $.
    seqcoll.4 $e |- ( ph -> A C_ ( ZZ>= ` M ) ) $.
    seqcoll.5 $e |- ( ( ph /\ k e. ( M ... ( G ` ( # ` A ) ) ) ) ->
                      ( F ` k ) e. S ) $.
    seqcoll.6 $e |- ( ( ph /\ k e. ( ( M ... ( G ` ( # ` A ) ) ) \ A ) ) ->
                      ( F ` k ) = Z ) $.
    seqcoll.7 $e |- ( ( ph /\ n e. ( 1 ... ( # ` A ) ) ) ->
                      ( H ` n ) = ( F ` ( G ` n ) ) ) $.
    $( The function ` F ` contains a sparse set of non-zero values to be
       summed.  The function ` G ` is an order isomorphism from the set of
       non-zero values of ` F ` to a 1-based finite sequence, and ` H `
       collects these non-zero values together.  Under these conditions, the
       sum over the values in ` H ` yields the same result as the sum over the
       original set ` F ` .  (Contributed by Mario Carneiro, 2-Apr-2014.) $)
    seqcoll $p |- ( ph -> ( seq M ( P , F ) ` ( G ` N ) ) =
                          ( seq 1 ( P , H ) ` N ) ) $=
      ( vy vm c1 chash cfv cfz co wcel cseq wceq cn wi elfznn caddc eleq1 fveq2
      syl cv fveq2d eqeq12d imbi12d imbi2d cuz cres wf wf1o clt wiso isof1o cvv
      f1of fvex elfzuz2 sylancr eluzfz1 ffvelrn syl2anc sseldd cle eluzle wn wb
      wbr eluzfz2 isorel syl12anc 1re ltnle sylancl eluzelz zre 3bitr3d con4bid
      cr cz mpbid mpbird expcom vtoclga mpcom cmin wss peano2zm ltm1 ltle letrd
      wa mpd eluz fzss2 ssel2 sylan 3syl nnre adantr breq1d mtbid ex con2d syl5
      imp sylanbrc seq1 ax-mp ad2antlr elfzuz3 elfzuzb ad2antrr zltlem1 elfzle2
      syldan adantl elfzelz uztrn adantrr 3bitrd cc a2d eluzelre elfz5 cdif w3a
      eleq1d eluzel2 simp3 ccnv f1ocnv nnge1 lenlt f1ocnvfv2 bitrd sylbid eldif
      elfzm11 seqid fveq1d uzid fvres eqtr4d 3eqtr3d 1z syl6eqr a1d simplr nnuz
      syl6eleq simpr peano2uzr imim1d oveq1 simpll ltp1 simplll elfzuz peano2uz
      nnz syl2anr ovex mpan syl2an elfzle1 jca simprr btwnnz 3expib sylc breq2d
      seqcl zltp1le anbi12d expr seqid2 oveq1d impcom adantlr oveq2d zcn ax-1cn
      npcan eluzp1p1 eqeltrrd seqm1 3eqtr4rd seqp1 syl5ibr syld nnind ) AKUEBUF
      UGZUHUIZUJZKHUGZCGJUKZUGZKCIUEUKZUGZULZRKUMUJZAUXLUXRUNZAUXLUXSRKUXJUOUSA
      UCUTZUXKUJZUYAHUGZUXNUGZUYAUXPUGZULZUNZUNAUEUXKUJZUEHUGZUXNUGZUEUXPUGZULZ
      UNZUNAUDUTZUXKUJZUYNHUGZUXNUGZUYNUXPUGZULZUNZUNAUYNUEUPUIZUXKUJZVUAHUGZUX
      NUGZVUAUXPUGZULZUNZUNAUXTUNUCUDKUYAUEULZUYGUYMAVUHUYBUYHUYFUYLUYAUEUXKUQV
      UHUYDUYJUYEUYKVUHUYCUYIUXNUYAUEHURVAUYAUEUXPURVBVCVDUYAUYNULZUYGUYTAVUIUY
      BUYOUYFUYSUYAUYNUXKUQVUIUYDUYQUYEUYRVUIUYCUYPUXNUYAUYNHURVAUYAUYNUXPURVBV
      CVDUYAVUAULZUYGVUGAVUJUYBVUBUYFVUFUYAVUAUXKUQVUJUYDVUDUYEVUEVUJUYCVUCUXNU
      YAVUAHURVAUYAVUAUXPURVBVCVDUYAKULZUYGUXTAVUKUYBUXLUYFUXRUYAKUXKUQVUKUYDUX
      OUYEUXQVUKUYCUXMUXNUYAKHURVAUYAKUXPURVBVCVDAUYLUYHAUYJUEIUGZUYKAUYIUXNUYI
      VEUGZVFZUGZUYICGUYIUKZUGZUYJVULAUYIVUNVUPAECDGJUYILMPABJVEUGZUYISAUXKBHVG
      ZUYHUYIBUJAUXKBHVHZVUSAUXKBVIVIHVJZVUTQUXKBVIVIHVKUSZUXKBHVMUSZAUXJUEVEUG
      ZUJZUYHAUXJVLUJZUXLVVEBUFVNZRVLKUEUXJVOVPZUEUXJVQUSZUXKBUEHVRVSVTZUYIJUXJ
      HUGZUHUIZUJZAUYIGUGZDUJZAVVMUYIVVKWAWEZAUEUXJWAWEZVVPAVVEVVQVVHUEUXJWBUSA
      VVQVVPAUXJUEVIWEZVVKUYIVIWEZVVQWCZVVPWCZAVVAUXJUXKUJZUYHVVRVVSWDQAVVEVWBV
      VHUEUXJWFUSZVVIUXKBUXJUEVIVIHWGWHAUXJWPUJZUEWPUJZVVRVVTWDAVVEVWDVVHUEUXJU
      UAUSZWIUXJUEWJWKAVVKWPUJZUYIWPUJZVVSVWAWDAVVKWQUJZVWGAVVKVURUJVWIABVURVVK
      SAVUSVWBVVKBUJVVCVWCUXKBUXJHVRVSVTJVVKWLUSZVVKWMUSZAUYIWQUJZVWHAUYIVURUJZ
      VWLVVJJUYIWLUSZUYIWMUSZVVKUYIWJVSWNWOWRZAVWMVWIVVMVVPWDVVJVWJUYIJVVKUUBVS
      WSAEUTZGUGZDUJZUNAVVOUNEUYIVVLVWQUYIULZVWSVVOAVWTVWRVVNDVWQUYIGURUUEVDAVW
      QVVLUJZVWSTWTXAXBAVWQJUYIUEXCUIZUHUIZUJZVWQVVLBUUCUJZVWRLULZAVXDXIVXAVWQB
      UJZWCZVXEAVXCVVLXDZVXDVXAAVVKVXBVEUGUJZVXIAVXJVXBVVKWAWEZAVXBUYIVVKAVXBWQ
      UJZVXBWPUJZAVWLVXLVWNUYIXEUSZVXBWMUSZVWOVWKAVXBUYIVIWEZVXBUYIWAWEZAVWHVXP
      VWOUYIXFUSAVXMVWHVXPVXQUNVXOVWOVXBUYIXGVSXJVWPXHAVXLVWIVXJVXKWDVXNVWJVXBV
      VKXKVSWSVXBJVVKXLUSVXCVVLVWQXMXNAVXDVXHAVXDVWQWQUJZJVWQWAWEZVWQUYIVIWEZUU
      DZVXHAJWQUJZVWLVXDVYAWDAVWMVYBVVJJUYIUUFUSZVWNVWQJUYIUUPVSVYAVXTAVXHVXRVX
      SVXTUUGAVXGVXTAVXGVXTWCAVXGXIZVWQHUUHZUGZUEVIWEZVXTVYDVYFUXKUJZVYFUMUJZVY
      GWCZABUXKVYEVGZVXGVYHAVUTBUXKVYEVHVYKVVBUXKBHUUIBUXKVYEVMXOZBUXKVWQVYEVRZ
      XNZVYFUXJUOVYIUEVYFWAWEZVYJVYFUUJVYIVWEVYFWPUJVYOVYJWDWIVYFXPUEVYFUUKVPWR
      XOVYDVYGVYFHUGZUYIVIWEZVXTVYDVVAVYHUYHVYGVYQWDAVVAVXGQXQVYNAUYHVXGVVIXQUX
      KBVYFUEVIVIHWGWHVYDVYPVWQUYIVIAVUTVXGVYPVWQULZVVBUXKBVWQHUULZXNXRUUMXSXTY
      AYBUUNYCVWQVVLBUUOZYDUAYMUUQUURAUYIVUMUJZVUOUYJULAVWLWUAVWNUYIUUSUSUYIVUM
      UXNUUTUSAVUQVVNVULAVWLVUQVVNULVWNCGUYIYEUSUYHAVULVVNULZVVIAFUTZIUGZWUCHUG
      ZGUGZULZUNZAWUBUNFUEUXKWUCUEULZWUGWUBAWUIWUDVULWUFVVNWUCUEIURWUIWUEUYIGWU
      CUEHURVAVBVDAWUCUXKUJWUGUBWTZXAXBUVAUVBUEWQUJUYKVULULUVCCIUEYEYFUVDUVEUYN
      UMUJZAUYTVUGAWUKUYTVUGUNAWUKXIZUYTVUBUYSUNVUGWULVUBUYOUYSWULVUBUYOWULVUBX
      IZUYNVVDUJZUXJUYNVEUGUJZUYOWUMUYNUMVVDAWUKVUBUVFUVGUVHZWUMUYNWQUJZUXJVUAV
      EUGUJZWUOWUKWUQAVUBUYNUVRZYGWUMVVFVUBWURVVGWULVUBUVIZVLVUAUEUXJYHVPUYNUXJ
      UVJVSVVFUYOWUNWUOXIWDVVGVLUYNUEUXJYIYFYDZXTUVKWULVUBUYSVUFWULVUBUYSVUFUNU
      YSVUFWUMUYQVUAIUGZCUIZUYRWVBCUIZULUYQUYRWVBCUVLWUMVUDWVCVUEWVDWUMUYQVUCGU
      GZCUIVUCUEXCUIZUXNUGZWVECUIZWVCVUDWUMUYQWVGWVECWUMECDGUYPJWVFLWUMAVWQDUJZ
      VWQLCUIVWQULAWUKVUBUVMZNXNWUMBVURUYPABVURXDZWUKVUBSYJZWUMVUSUYOUYPBUJAVUS
      WUKVUBVVCYJZWVAUXKBUYNHVRVSVTZWUMWVFUYPVEUGZUJZUYPWVFWAWEZWUMUYPVUCVIWEZW
      VQWUMUYNVUAVIWEZWVRWUMUYNWPUJZWVSWUKWVTAVUBUYNXPYGUYNUVNUSWUMVVAUYOVUBWVS
      WVRWDAVVAWUKVUBQYJZWVAWUTUXKBUYNVUAVIVIHWGWHWRWUMUYPWQUJZVUCWQUJZWVRWVQWD
      WUMUYPVURUJZWWBWVNJUYPWLUSZWUMVUCVURUJWWCWUMBVURVUCWVLWUMVUSVUBVUCBUJWVMW
      UTUXKBVUAHVRVSVTJVUCWLUSZUYPVUCYKVSWRWUMWWBWVFWQUJZWVPWVQWDWWEWUMWWCWWGWW
      FVUCXEUSZUYPWVFXKVSWSZWUMEFCDGJUYPWVNWUMVWQJUYPUHUIZUJZVXAVWSWUMWWJVVLXDZ
      WWKVXAWUMVVKWVOUJZWWLWUMVVKWVFVEUGUJZWVPWWMWUMWWNWVFVVKWAWEZWUMWVFVUCVVKW
      UMWWGWVFWPUJZWWHWVFWMUSZWUMWWCVUCWPUJZWWFVUCWMUSZAVWGWUKVUBVWKYJZWUMWVFVU
      CVIWEZWVFVUCWAWEZWUMWWRWXAWWSVUCXFUSWUMWWPWWRWXAWXBUNWWQWWSWVFVUCXGVSXJWU
      MVUAUXJWAWEZVUCVVKWAWEZVUBWXCWULVUAUEUXJYLYNWUMWXCWXDWUMUXJVUAVIWEZVVKVUC
      VIWEZWXCWCZWXDWCZWUMVVAVWBVUBWXEWXFWDWWAAVWBWUKVUBVWCYJWUTUXKBUXJVUAVIVIH
      WGWHWUMVWDVUAWPUJZWXEWXGWDAVWDWUKVUBVWFYJWUMVUAWQUJZWXIVUBWXJWULVUAUEUXJY
      OYNVUAWMUSUXJVUAWJVSWUMVWGWWRWXFWXHWDWWTWWSVVKVUCWJVSWNWOWRXHWUMWWGVWIWWN
      WWOWDWWHAVWIWUKVUBVWJYJWVFVVKXKVSWSZWWIWVFVVKUYPYPVSUYPJVVKXLUSWWJVVLVWQX
      MXNWUMAVXAVWSWVJTXNYMWUMAWVIWUCDUJXIVWQWUCCUIDUJWVJOXNUWJWUMVWQUYPUEUPUIZ
      WVFUHUIUJZXIZAVXEVXFAWUKVUBWXMUVOWXNVXAVXHVXEWXNVWQVURUJZVVKVWQVEUGZUJZVX
      AWXMVWQWXLVEUGUJWXLVURUJZWXOWUMVWQWXLWVFUVPWUMWWDWXRWVNJUYPUVQUSWXLVWQJYP
      UVSWUMWWNWVFWXPUJZWXQWXMWXKWVFVLUJWXMWXSVUCUEXCUVTVLVWQWXLWVFYHUWAWVFVVKV
      WQYPUWBVVKVLUJVXAWXOWXQXIWDUXJHVNVLVWQJVVKYIYFYDWUMWXMVXHWXMWXLVWQWAWEZVW
      QWVFWAWEZXIZWUMVXHWXMWXTWYAVWQWXLWVFUWCVWQWXLWVFYLUWDWUMVXGWYBWULVUBVXGWY
      BWCWULVUBVXGXIZXIZUYNVYFVIWEZVYFVUAVIWEZXIZWYBWYDWUQVYFWQUJZWYGWCWUKWUQAW
      YCWUSYGWYDVYHWYHWYDVYKVXGVYHAVYKWUKWYCVYLYJWULVUBVXGUWEZVYMVSZVYFUEUXJYOU
      SWUQWYGWYHWUQWYEWYFWYHWCUYNVYFUWFUWGYAUWHWYDWYEWXTWYFWYAWYDWYEUYPVYPVIWEZ
      UYPVWQVIWEZWXTWYDVVAUYOVYHWYEWYKWDAVVAWUKWYCQYJZWULVUBUYOVXGWVAYQWYJUXKBU
      YNVYFVIVIHWGWHWYDVYPVWQUYPVIWYDVUTVXGVYRAVUTWUKWYCVVBYJWYIVYSVSZUWIWYDWWB
      VXRWYLWXTWDWULVUBWWBVXGWWEYQWYDWXOVXRWYDBVURVWQAWVKWUKWYCSYJWYIVTJVWQWLUS
      ZUYPVWQUWKVSYRWYDWYFVYPVUCVIWEZVWQVUCVIWEZWYAWYDVVAVYHVUBWYFWYPWDWYMWYJWU
      LVUBVUBVXGWUTYQUXKBVYFVUAVIVIHWGWHWYDVYPVWQVUCVIWYNXRWYDVXRWWCWYQWYAWDWYO
      WULVUBWWCVXGWWFYQVWQVUCYKVSYRUWLXSUWMYAYBYCVYTYDUAVSUWNUWOWUMWVBWVEUYQCAV
      UBWVBWVEULZWUKVUBAWYRWUHAWYRUNFVUAUXKWUCVUAULZWUGWYRAWYSWUDWVBWUFWVEWUCVU
      AIURWYSWUEVUCGWUCVUAHURVAVBVDWUJXAUWPUWQUWRWUMVYBVUCJUEUPUIVEUGZUJVUDWVHU
      LAVYBWUKVUBVYCYJWUMWVFUEUPUIZVUCWYTWUMVUCYSUJZUEYSUJXUAVUCULWUMWWCXUBWWFV
      UCUWSUSUWTVUCUEUXAWKWUMWVFVURUJZXUAWYTUJWUMWVPWWDXUCWWIWVNUYPWVFJYPVSJWVF
      UXBUSUXCCGJVUCUXDVSUXEWUMWUNVUEWVDULWUPCIUEUYNUXFUSVBUXGXTYTUXHWTYTUXIXBX
      J $.
      $( [2-Apr-2014] $)
  $}

