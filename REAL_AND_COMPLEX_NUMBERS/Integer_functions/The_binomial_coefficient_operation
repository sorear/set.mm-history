$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial coefficient operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c _C $. $( Binomial coefficient operation. $)

  $( Extend class notation to include the binomial coefficient operation
     (combinatorial choose operation). $)
  cbc $a class _C $.

  ${
    $d n k m $.
    $( Define the binomial coefficient operation.  In the literature, this
       function is often written as a column vector of the two arguments, or
       with the arguments as subscripts before and after the letter "C".
       ` ( N _C K ) ` is read " ` N ` choose ` K ` ."  Definition of binomial
       coefficient in [Gleason] p. 295.  As suggested by Gleason, we define it
       to be 0 when ` 0 <_ k <_ n ` does not hold. $)
    df-bc $a |- _C = ( n e. NN0 , k e. ZZ |-> if ( k e. ( 0 ... n ) ,
                ( ( ! ` n ) / ( ( ! ` ( n - k ) ) x. ( ! ` k ) ) ) , 0 ) ) $.
  $}

  ${
    $d n k m N $.  $d n k m K $.
    $( Value of the binomial coefficient, ` N ` choose ` K ` .  Definition of
       binomial coefficient in [Gleason] p. 295.  As suggested by Gleason, we
       define it to be 0 when ` 0 <_ K <_ N ` does not hold.  See ~ bcval2 for
       the value in the standard domain. $)
    bcval $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) = if ( K e. ( 0 ... N )
      , ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) , 0 ) ) $=
      ( vn vk cv cc0 cfz co wcel cfa cfv cmin cmul cdiv wceq oveq2 fveq2 fveq2d
      cif oveq12d cn0 cz eleq2d oveq1 oveq1d eqidd ifbieq12d eleq1 oveq2d df-bc
      cbc ovex cc 0cn elexi ifex ovmpt2 ) CDBAUAUBDEZFCEZGHZIZUSJKZUSURLHZJKZUR
      JKZMHZNHZFSAFBGHZIZBJKZBALHZJKZAJKZMHZNHZFSUKURVHIZVJBURLHZJKZVEMHZNHZFSU
      SBOZVAVPVGFVTFWAUTVHURUSBFGPUCWAVBVJVFVSNUSBJQWAVDVRVEMWAVCVQJUSBURLUDRUE
      TWAFUFUGURAOZVPVIVTFVOFURAVHUHWBVSVNVJNWBVRVLVEVMMWBVQVKJURABLPRURAJQTUIW
      BFUFUGDCUJVIVOFVJVNNULFUMUNUOUPUQ $.
      $( [7-Nov-2013] $) $( [10-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) ) $=
      ( wcel cc0 cfz co cbc cfa cfv cmin cmul cdiv cif cn0 wceq elfz3nn0 adantl
      wa cz elfzelz bcval syl2anc iftrue eqtrd ) BCDZAEBFGDZSZBAHGZUGBIJBAKGIJA
      IJLGMGZENZUJUHBODATDZUIUKPCABQUGULUFAEBUARABUBUCUGUKUJPUFUGUJEUDRUE $.
      $( [7-Nov-2013] $) $( [9-Jun-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval3 $p |- ( ( N e. NN0 /\ K e. ZZ /\ -. K e. ( 0 ... N ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 cfz co wn w3a cbc cfa cfv cmin cmul cdiv cif wceq bcval
      3adant3 iffalse 3ad2ant3 eqtrd ) BCDZAEDZAFBGHDZIZJBAKHZUFBLMBANHLMALMOHP
      HZFQZFUDUEUHUJRUGABSTUGUDUJFRUEUFUIFUAUBUC $.
      $( [8-Nov-2013] $) $( [14-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval4 $p |- ( ( N e. NN0 /\ K e. ZZ /\ ( K < 0 \/ N < K ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 clt wbr wo cfz co wn cbc wa cle cr lenlt mpbid adantl
      wb wceq wi elfzle1 0re elfzelz zre sylancr elfzle2 nn0re syl2anr sylanbrc
      syl ioran ex adantr con2d 3impia bcval3 syld3an3 ) BCDZAEDZAFGHZBAGHZIZAF
      BJKDZLZBAMKFUAUTVAVDVFUTVANVEVDUTVEVDLZUBVAUTVEVGUTVENZVBLZVCLZVGVEVIUTVE
      FAOHZVIAFBUCVEFPDAPDZVKVITUDVEVAVLAFBUEAUFULZFAQUGRSVHABOHZVJVEVNUTAFBUHS
      VEVLBPDVNVJTUTVMBUIABQUJRVBVCUMUKUNUOUPUQABURUS $.
      $( [7-Nov-2013] $) $( [14-Jul-2005] $)
  $}

  $( Closure of the binomial coefficient in the positive reals.  (This is
     mostly a lemma before we have ~ bccl2 .)  (Contributed by Mario Carneiro,
     10-Mar-2014.) $)
  bcrpcl $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. RR+ ) $=
    ( wcel cc0 cfz co wa cbc cfa cfv cmin cmul crp cn faccl syl2an syl2anc nnrp
    cn0 cdiv bcval2 elfz3nn0 fznn0sub elfznn0 adantl nnmulcl rpdivcl eqeltrd
    syl ) BCDZAEBFGDZHZBAIGBJKZBALGZJKZAJKZMGZUAGZNABCUBUMUNODZURODZUSNDZUMBTDU
    TCABUCBPUJUMUOTDZATDZVACAEBUDULVDUKABUEUFVCUPODUQODVAVDUOPAPUPUQUGQRUTUNNDU
    RNDVBVAUNSURSUNURUHQRUI $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( "Complementing" its second argument doesn't change a binary coefficient.
     (Revised by Mario Carneiro, 5-Mar-2014.) $)
  bccmpl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) =
                    ( N _C ( N - K ) ) ) $=
    ( cn0 wcel cz wa cc0 co cbc cmin wceq cfa cmul cdiv cc fznn0sub2 cn elfznn0
    cfv faccl cfz nncn syl adantl mulcom syl2anc elfzelz nn0cn zcn nncan syl2an
    sylan2 fveq2d oveq1d eqtr4d oveq2d bcval2 syldan 3eqtr4d adantlr w3a bcval3
    3syl wn simp1 zsubcl sylan 3adant3 wi ex adantr eleq1d sylibd con3d syl3anc
    nn0z 3impia 3expa pm2.61dan ) BCDZAEDZFZAGBUAHZDZBAIHZBBAJHZIHZKZVTWDWHWAVT
    WDFZBLSZWFLSZALSZMHZNHWJBWFJHZLSZWKMHZNHZWEWGWIWMWPWJNWIWMWLWKMHZWPWIWKODZW
    LODZWMWRKWIWFWCDZWSCABPZXAWFCDWKQDWSWFBRWFTWKUBVCUCWIACDZWLQDWTWDXCVTABRUDA
    TWLUBVCWKWLUEUFWIWOWLWKMWIWNALWDVTWAWNAKZAGBUGVTBODAODXDWABUHAUIBAUJUKZULUM
    UNUOUPABCUQVTWDXAWGWQKXBWFBCUQURUSUTVTWAWDVDZWHVTWAXFVAZWEGWGABVBXGVTWFEDZX
    AVDZWGGKVTWAXFVEVTWAXHXFVTBEDWAXHBVPBAVFVGVHVTWAXFXIWBXAWDWBXAWNWCDZWDVTXAX
    JVIWAVTXAXJCWFBPVJVKWBWNAWCXEVLVMVNVQWFBVBVOUOVRVS $.
    $( [5-Mar-2014] $) $( [21-Jun-2005] $)

  $( ` N ` choose 0 is 1.  Remark in [Gleason] p. 296. $)
  bcn0 $p |- ( N e. NN0 -> ( N _C 0 ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cfa cfv cmin cmul cdiv c1 cfz wceq cuz eluzfz1 eleq2s
    nn0uz cc syl bcval2 mpdan nn0cn subid1 fveq2d fac0 oveq12 sylancl cn mulid1
    faccl nncn eqtrd oveq2d wne facne0 divid syl2anc 3eqtrd ) ABCZADEFZAGHZADIF
    ZGHZDGHZJFZKFZVBVBKFZLUTDDAMFCZVAVGNVIADOHBDAPRQDABUAUBUTVFVBVBKUTVFVBLJFZV
    BUTVDVBNVELNVFVJNUTVCAGUTASCVCANAUCAUDTUEUFVDVBVELJUGUHUTVBSCZVJVBNUTVBUICV
    KAUKVBULTZVBUJTUMUNUTVKVBDUOVHLNVLAUPVBUQURUS $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( ` N ` choose ` N ` is 1.  Remark in [Gleason] p. 296. $)
  bcnn $p |- ( N e. NN0 -> ( N _C N ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cmin c1 cz wceq 0z bccmpl mpan2 bcn0 nn0cn subid1 syl
    cc oveq2d 3eqtr3rd ) ABCZADEFZAADGFZEFZHAAEFUADICUBUDJKDALMANUAUCAAEUAARCUC
    AJAOAPQST $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( Binomial coefficient: ` N ` choose ` 1 ` . $)
  bcn1 $p |- ( N e. NN0 -> ( N _C 1 ) = N ) $=
    ( cn0 wcel cn cc0 wceq wo c1 cbc co cfa cfv cmul cdiv cuz syl nncn clt wbr
    cc elnn0 cmin cfz wa elnnuz biimpi 1nn0 nn0uz eleqtri elfzuzb mpbird bcval2
    jctil mpdan facnn2 fac1 oveq2i nnm1nn0 faccl mulid1 3syl syl5eq oveq12d wne
    nnne0 divcan3 syl3anc 3eqtrd cz 0nn0 1z lt01 olci bcval4 mp3an oveq1 eqeq12
    wb mpancom mpbiri jaoi sylbi ) ABCADCZAEFZGAHIJZAFZAUAWCWFWDWCWEAKLZAHUBJZK
    LZHKLZMJZNJZWIAMJZWINJZAWCHEAUCJCZWEWLFWCWOHEOLZCZAHOLCZUDWCWRWQWCWRAUEUFHB
    WPUGUHUIUMDHEAUJUKHADULUNWCWGWMWKWINAUOWCWKWIHMJZWIWJHWIMUPUQWCWIDCZWITCZWS
    WIFWCWHBCWTAURWHUSPZWIQZWIUTVAVBVCWCATCXAWIEVDZWNAFAQWCWTXAXBXCPWCWTXDXBWIV
    EPAWIVFVGVHWDWFEHIJZEFZEBCHVICHERSZEHRSZGXFVJVKXHXGVLVMHEVNVOWEXEFWDWFXFVRA
    EHIVPWEXEAEVQVSVTWAWB $.
    $( [8-Nov-2013] $) $( [21-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` N ` . $)
  bcnp1n $p |- ( N e. NN0 -> ( ( N + 1 ) _C N ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cbc cmin cz wceq peano2nn0 nn0z bccmpl syl2anc nn0cn
    cc ax-1cn pncan2 sylancl oveq2d bcn1 syl 3eqtrd ) ABCZADEFZAGFZUEUEAHFZGFZU
    EDGFZUEUDUEBCZAICUFUHJAKZALAUEMNUDUGDUEGUDAPCDPCUGDJAOQADRSTUDUJUIUEJUKUEUA
    UBUC $.
    $( [8-Nov-2013] $) $( [20-Jun-2005] $)

  $( The proportion of one binomial coefficient to another with ` K ` decreased
     by 1.  (Contributed by Mario Carneiro, 10-Mar-2014.) $)
  bcm1k $p |- ( ( N e. V /\ K e. ( 1 ... N ) ) -> ( N _C K ) =
                ( ( N _C ( K - 1 ) ) x. ( ( N - ( K - 1 ) ) / K ) ) ) $=
    ( wcel c1 cfz co cfa cfv cmul cdiv cc wceq syl nncn cc0 cn0 faccl syl2anc
    cn wa cmin cbc caddc cz elfzel2g zcn elfznn adantl ax-1cn a1i subsub fveq2d
    syl3anc oveq1d oveq2d oveq12d wne cuz elfzuz2 nnuz syl6eleqr nnnn0 fznn0sub
    nn0p1nn nn0cn nnm1nn0 nnmulcl nnne0 jca divmuldiv facp1 eqcomd facnn2 mul32
    mulass 3eqtr4d eqtr4d mulcom divcan5 syl122anc eqtrd 3eqtrrd addid2i oveq1i
    syl22anc wss 0z fzp1ss mpan syl5eqssr sselda bcval2 syldan sylancl peano2zm
    npcan uzid peano2uz 3syl eqeltrrd fzss2 simpr elfzelz elfzm1b mpbid sseldd
    wb ) BCDZAEBFGZDZUAZBHIZBAUBGZHIZAHIZJGZKGZXMBAEUBGZUBGZHIZXSHIZJGZKGZXTAKG
    ZJGZBAUCGZBXSUCGZYEJGXLYFXMXNEUDGZHIZYBJGZKGZYIAKGZJGZXMYIJGZXQYIJGZKGZXRXL
    YDYLYEYMJXLYCYKXMKXLYAYJYBJXLXTYIHXLBLDZALDZELDZXTYIMXLBUEDZYRCAEBUFZBUGNZX
    LATDZYSXKUUDXIABUHUIZAONZYTXLUJUKBAEULUNZUMUOUPXLXTYIAKUUGUOUQXLYNYOYKAJGZK
    GZYQXLXMLDZYILDZYKLDZYKPURZUAZYSAPURZUAYNUUIMXLXMTDZUUJXLBQDZUUPXLBTDUUQXLB
    EUSITCAEBUTVAVBBVCNBRNXMONZXLYIQDZUUKXLYITDZUUSXLXNQDZUUTCAEBVDZXNVENZYIVCN
    ZYIVFNZXLYKTDZUUNXLYJTDZYBTDZUVFXLUUSUVGUVDYIRNZXLXSQDZUVHXLUUDUVJUUEAVGNXS
    RNZYJYBVHSUVFUULUUMYKOYKVIVJNXLYSUUOUUFXLUUDUUOUUEAVINVJXMYIYKAVKWFXLYPUUHY
    OKXLXOYIJGZXPJGZYJYBAJGZJGZYPUUHXLUVLYJXPUVNJXLYJUVLXLUVAYJUVLMUVBXNVLNVMXL
    UUDXPUVNMUUEAVNNUQXLXOLDZXPLDZUUKYPUVMMXLXOTDZUVPXLUVAUVRUVBXNRNZXOONXLXPTD
    ZUVQXLAQDZUVTXLUUDUWAUUEAVCNARNZXPONUVEXOXPYIVOUNXLYJLDZYBLDZYSUUHUVOMXLUVG
    UWCUVIYJONXLUVHUWDUVKYBONUUFYJYBAVPUNVQUPVRXLYQYIXMJGZYIXQJGZKGZXRXLYOUWEYP
    UWFKXLUUJUUKYOUWEMUURUVEXMYIVSSXLXQLDZUUKYPUWFMXLXQTDZUWHXLUVRUVTUWIUVSUWBX
    OXPVHSZXQONZUVEXQYIVSSUQXLUUJUWHXQPURZUUKYIPURZUWGXRMUURUWKXLUWIUWLUWJXQVIN
    UVEXLUUTUWMUVCYIVINXMXQYIVTWAWBWCXIXKAPBFGZDYGXRMXIXJUWNAXIXJPEUDGZBFGZUWNU
    WOEBFEUJWDWEPUEDXIUWPUWNWGWHPBCWIWJWKWLABCWMWNXLYHYDYEJXIXKXSUWNDYHYDMXLPBE
    UBGZFGZUWNXSXLBUWQUSIZDUWRUWNWGXLUWQEUDGZBUWSXLYRYTUWTBMUUCUJBEWQWOXLUWQUED
    ZUWQUWSDUWTUWSDXLUUAUXAUUBBWPNUWQWRUWQUWQWSWTXAUWQPBXBNXLXKXSUWRDZXIXKXCXLA
    UEDZUUAXKUXBXHXKUXCXIAEBXDUIUUBABXESXFXGXSBCWMWNUOVQ $.
    $( [10-Mar-2014] $)

  $( The proportion of one binomial coefficient to another with ` N ` increased
     by 1.  (Contributed by Mario Carneiro, 10-Mar-2014.) $)
  bcp1n $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( ( N + 1 ) _C K ) =
                ( ( N _C K ) x. ( ( N + 1 ) / ( ( N + 1 ) - K ) ) ) ) $=
    ( wcel cc0 cfz co wa c1 cfa cfv cmul cdiv cn0 wceq syl cc cn faccl nncn cbc
    caddc cmin elfz3nn0 facp1 fznn0sub ax-1cn a1i elfznn0 adantl addsub syl3anc
    nn0cn fveq2d oveq2d 3eqtr4d nn0p1nn eqeltrd mul32 eqtrd oveq12d wne nnmulcl
    oveq1d syl2anc nnne0 jca divmuldiv syl22anc eqtr4d fzelp1 bcval2 ) BCDZAEBF
    GDZHZBIUBGZJKZVPAUCGZJKZAJKZLGZMGZBJKZBAUCGZJKZVTLGZMGZVPVRMGZLGZVPAUAGZBAU
    AGZWHLGVOWBWCVPLGZWFVRLGZMGZWIVOVQWLWAWMMVOBNDZVQWLOCABUDZBUEPVOWAWEVRLGZVT
    LGZWMVOVSWQVTLVOWDIUBGZJKZWEWSLGZVSWQVOWDNDZWTXAOCAEBUFZWDUEPVOVRWSJVOBQDZI
    QDZAQDZVRWSOVOWOXDWPBUMPXEVOUGUHVOANDZXFVNXGVMABUIUJZAUMPBIAUKULZUNVOVRWSWE
    LXIUOUPVDVOWEQDZVRQDZVTQDZWRWMOVOWERDZXJVOXBXMXCWDSPZWETPVOVRRDZXKVOVRWSRXI
    VOXBWSRDXCWDUQPURZVRTPZVOVTRDZXLVOXGXRXHASPZVTTPWEVRVTUSULUTVAVOWCQDZVPQDZW
    FQDZWFEVBZHZXKVREVBZHWIWNOVOWCRDZXTVOWOYFWPBSPWCTPVOVPRDZYAVOWOYGWPBUQPVPTP
    ZVOWFRDZYDVOXMXRYIXNXSWEVTVCVEYIYBYCWFTWFVFVGPVOXKYEXQVOXOYEXPVRVFPVGWCVPWF
    VRVHVIVJVOYAAEVPFGDWJWBOYHCAEBVKAVPQVLVEVOWKWGWHLABCVLVDUP $.
    $( [10-Mar-2014] $)

  $( The proportion of one binomial coefficient to another with ` N ` and ` K `
     increased by 1.  (Contributed by Mario Carneiro, 16-Jan-2015.) $)
  bcp1nk $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( ( N + 1 ) _C ( K + 1 ) ) =
                ( ( N _C K ) x. ( ( N + 1 ) / ( K + 1 ) ) ) ) $=
    ( wcel cc0 cfz co c1 caddc cbc cmin cdiv cmul cz wceq syl2anc cc syl recnd
    cr wa elfzel2g peano2zdi simpr wb 0z a1i elfzelz adantl 1z fzaddel syl22anc
    mpbid 0p1e1 oveq1i syl6eleqr bcm1k ax-1cn pncan sylancl oveq2d bcp1n oveq1d
    zcn eqtrd oveq12d crp bcrpcl rpcn cn zre clt wbr wi idi cle elfzle2 lelttrd
    ltp1 znnsub nndivre nnre cn0 elfznn0 nn0p1nn mulass syl3anc mulcom wne nncn
    nnne0 dmdcan syl221anc 3eqtrd ) BCDZAEBFGDZUAZBHIGZAHIGZJGZWRWSHKGZJGZWRXAK
    GZWSLGZMGZBAJGZWRWRAKGZLGZMGZXGWSLGZMGZXFWRWSLGZMGZWQWRNDZWSHWRFGZDWTXEOWQB
    CAEBUBZUCZWQWSEHIGZWRFGZXOWQWPWSXSDZWOWPUDWQENDZBNDZANDZHNDZWPXTUEYAWQUFUGX
    PWPYCWOAEBUHUIZYDWQUJUGAHEBUKULUMHXRWRFUNUOUPWSWRNUQPWQXBXIXDXJMWQXBWRAJGXI
    WQXAAWRJWQAQDZHQDXAAOWQYCYFYEAVDRURAHUSUTZVAABCVBVEWQXCXGWSLWQXAAWRKYGVAVCV
    FWQXKXFXHXJMGZMGZXMWQXFQDZXHQDZXJQDZXKYIOWQXFVGDYJABCVHXFVIRWQXHWQWRTDZXGVJ
    DZXHTDWQXNYMXQWRVKRZWQAWRVLVMZYNWQABWRWQYCATDWQYCVNYEVOAVKRWQYBBTDZXPBVKRZY
    OWPABVPVMWOAEBVQUIWQYQBWRVLVMYRBVSRVRWQYCXNYPYNUEYEXQAWRVTPUMZWRXGWAPSZWQXJ
    WQXGTDZWSVJDZXJTDWQYNUUAYSXGWBRZWQAWCDZUUBWPUUDWOABWDUIAWERZXGWSWAPSZXFXHXJ
    WFWGWQYHXLXFMWQYHXJXHMGZXLWQYKYLYHUUGOYTUUFXHXJWHPWQXGQDXGEWIZWSQDZWSEWIZWR
    QDUUGXLOWQXGUUCSWQYNUUHYSXGWKRWQUUBUUIUUEWSWJRWQUUBUUJUUEWSWKRWQWRYOSXGWSWR
    WLWMVEVAVEWN $.
    $( [16-Jan-2015] $)

  ${
    $d k x y K $.  $d k x y N $.
    $( Write out the top and bottom parts of the binomial coefficient
       ` ( N _C K ) = ( N x. ( N - 1 ) x. ... x. ( ( N - K ) + 1 ) ) / K ! `
       explicitly.  In this form, it is valid even for ` N < K ` , although it
       is no longer valid for non-positive ` K ` .  (Contributed by Mario
       Carneiro, 22-May-2014.) $)
    bcval5 $p |- ( ( N e. NN0 /\ K e. NN ) -> ( N _C K ) =
                 ( ( seq ( ( N - K ) + 1 ) ( x. , _I ) ` N ) / ( ! ` K ) ) ) $=
      ( cn0 wcel cn wa cc0 co cmul cid c1 cfv cdiv cc adantl wbr syl2anc wb syl
      wceq vk vx vy cfz cbc cmin caddc cseq bcval2 adantlr fznn0sub elnn0 sylib
      cfa wo cv mulcl w3a mulass cuz cle clt simplr elfzuz3 nnuz uztrn2 adantrr
      cr crp nnre ltsubrp syl2an cz nnz ad2antlr zsubcl zltp1le mpbid peano2zdi
      nnrp mpbird simprr syl6eleq fvi elfzelz zcn eqeltrd seqsplit facnn oveq1d
      eluz 3eqtr4d expr simpll faccl nncn 3syl mulid2 oveq2d eqtr3d fac0 syl6eq
      fveq2 oveq1 ax-1cn addid2i seqeq1d fveq1d oveq12d syl5ibrcom jaod mpd wne
      eqeq2d wf eqid nn0z adantr eluzelz seqf ffvelrn elfznn0 divcan5 syl122anc
      nnne0 3eqtrd wn nnnn0 mul02 mul01 simpr nn0uz ad2antrr elfz5 nn0re subge0
      div0 bitr4d sylancl 0z mtbid zre 0re ltnle a1i elfz syl3anc mpbir2and 0cn
      nn0ge0 mp1i seqz bcval3 syl3an2 3expa 3eqtr4rd pm2.61dan ) BCDZAEDZFZAGBU
      DHDZBAUEHZBIJBAUFHZKUGHZUHZLZAUNLZMHZTUUTUVAFZUVBBUNLZUVCUNLZUVGIHZMHZUVK
      UVFIHZUVLMHZUVHUURUVAUVBUVMTUUSABCUIUJUVIUVJUVNUVLMUVIUVCEDZUVCGTZUOZUVJU
      VNTZUVIUVCCDZUVRUURUVAUVTUUSCAGBUKUJZUVCULUMUVIUVPUVSUVQUUTUVAUVPUVSUUTUV
      AUVPFZFZBIJKUHZLZUVCUWDLZUVFIHUVJUVNUWCUAUBUCINJKUVCBUAUPZNDZUBUPZNDZFZUW
      GUWIIHZNDZUWCUWGUWIUQZOUWHUWJUCUPZNDURUWLUWOIHUWGUWIUWOIHIHTUWCUWGUWIUWOU
      SOUWCBUVDUTLZDZUVDBVAPZUWCUVCBVBPZUWRUWCBEDZUUSUWSUUTUVAUWTUVPUVIUUSBAUTL
      DZUWTUURUUSUVAVCZUURUVAUXAUUSCAGBVDUJKBAEVEVFQZVGZUURUUSUWBVCUWTBVHDZAVID
      UWSUUSBVJAVTBAVKVLZQUWCUVCVMDZBVMDZUWSUWRRZUWCUXHAVMDZUXGUWCUWTUXHUXDBVNZ
      SZUUSUXJUURUWBAVNZVOBAVPZQZUXLUVCBVQZQVRUWCUVDVMDZUXHUWQUWRRZUWCUVCUXOVSU
      XLUVDBWKZQWAUWCUVCEKUTLUUTUVAUVPWBZVEWCUWGKBUDHZDZUWGJLZNDZUWCUYBUYCUWGNU
      WGUYAWDUYBUWGVMDZUWHUWGKBWEUWGWFZSWGOWHUWCUWTUVJUWETZUXDBWIZSUWCUVKUWFUVF
      IUWCUVPUVKUWFTUXTUVCWISWJWLWMUVIUVSUVQUVJKUWEIHZTUVIKUVJIHZUVJUYIUVIUVJND
      ZUYJUVJTUVIUURUVJEDUYKUURUUSUVAWNBWOUVJWPWQUVJWRSUVIUVJUWEKIUVIUWTUYGUXCU
      YHSWSWTUVQUVNUYIUVJUVQUVKKUVFUWEIUVQUVKGUNLKUVCGUNXCXAXBUVQBUVEUWDUVQUVDK
      IJUVQUVDGKUGHKUVCGKUGXDKXEXFXBXGXHXIXNXJXKXLWJUVIUVFNDZUVGNDZUVGGXMZUVKND
      ZUVKGXMZUVOUVHTUVIUWPNUVEXOUWQUYLUVIUAUBINJUVDUWPUWPXPUUTUXQUVAUUTUVCUURU
      XHUXJUXGUUSBXQZUXMUXNVLZVSZXRZUWGUWPDZUYDUVIVUAUYCUWGNUWGUWPWDVUAUYEUWHUV
      DUWGXSUYFSWGOUWKUWMUVIUWNOXTUVIUWQUWRUVIUWSUWRUVIUWTUUSUWSUXCUXBUXFQUVIUX
      GUXHUXIUUTUXGUVAUYRXRUVIUWTUXHUXCUXKSZUXPQVRUVIUXQUXHUXRUYTVUBUXSQWAUWPNB
      UVEYAQUVIUVGEDZUYMUVIACDZVUCUVAVUDUUTABYBOAWOZSZUVGWPZSUVIVUCUYNVUFUVGYEZ
      SUVIUVKEDZUYOUVIUVTVUIUWAUVCWOSZUVKWPSUVIVUIUYPVUJUVKYESUVFUVGUVKYCYDYFUU
      TUVAYGZFZGUVGMHZGUVHUVBVULVUCVUMGTZVULUUSVUDVUCUURUUSVUKVCAYHZVUEWQVUCUYM
      UYNVUNVUGVUHUVGYQQSVULUVFGUVGMVULUAUBINJGUVDBCGUWKUWMVULUWNOUWGUVDBUDHZDZ
      UYDVULVUQUYCUWGNUWGVUPWDVUQUYEUWHUWGUVDBWEUYFSWGOUWHGUWGIHGTVULUWGYIOUWHU
      WGGIHGTVULUWGYJOVULGVUPDZUVDGVAPZGBVAPZVULUVCGVBPZVUSVULVVAGUVCVAPZYGZVUL
      UVAVVBUUTVUKYKVULUVAABVAPZVVBVULAGUTLZDUXHUVAVVDRVULACVVEUUSVUDUURVUKVUOV
      OYLWCUURUXHUUSVUKUYQYMZAGBYNQVULUXEAVHDZVVBVVDRUURUXEUUSVUKBYOYMUUSVVGUUR
      VUKAVJVOBAYPQYRUUAVULUVCVHDZGVHDVVAVVCRVULUXGVVHUUTUXGVUKUYRXRZUVCUUBSUUC
      UVCGUUDYSWAVULUXGGVMDZVVAVUSRVVIYTUVCGVQYSVRUURVUTUUSVUKBUUJYMVULVVJUXQUX
      HVURVUSVUTFRVVJVULYTUUEUUTUXQVUKUYSXRVVFGUVDBUUFUUGUUHUURUUSVUKWNGNDGJLGT
      VULUUIGNWDUUKUULWJUURUUSVUKUVBGTZUUSUURUXJVUKVVKUXMABUUMUUNUUOUUPUUQ $.
      $( [22-May-2014] $)
  $}

  $( Binomial coefficient: ` N ` choose ` 2 ` .  (Contributed by Mario
     Carneiro, 22-May-2014.) $)
  bcn2 $p |- ( N e. NN0 -> ( N _C 2 ) = ( ( N x. ( N - 1 ) ) / 2 ) ) $=
    ( cn0 wcel c2 co cmul cid cmin c1 caddc cseq cfv cdiv wceq ax-1cn cc syl cz
    cuz eqtrd cbc cfa 2nn bcval5 mpan2 2cn 1p1e2 subaddrii oveq2i nn0cn mp3an23
    cn npncan syl5eqr seqeq1d fveq1d nn0z peano2zm uzid sylancl fveq2d eleqtrrd
    npcan seqm1 syl2anc seq1 fvi oveq12d subcl mulcom 3eqtrd fac2 a1i ) ABCZADU
    AEZAFGADHEZIJEZKZLZDUBLZMEZAAIHEZFEZDMEVNDULCVOWANUCDAUDUEVNVSWCVTDMVNVSAFG
    WBKZLZWBAFEZWCVNAVRWDVNVQWBFGVNVQVPDIHEZJEZWBWGIVPJDIIUFOOUGUHUIVNAPCZWHWBN
    ZAUJZWIDPCIPCZWJUFOADIUMUKQUNUOUPVNWEWBWDLZAGLZFEZWFVNWBRCZAWBIJEZSLZCWEWON
    VNARCZWPAUQZAURQZVNAASLZWRVNWSAXBCWTAUSQVNWQASVNWIWLWQANWKOAIVCUTVAVBFGWBAV
    DVEVNWMWBWNAFVNWMWBGLZWBVNWPWMXCNXAFGWBVFQVNWPXCWBNXAWBRVGQTABVGVHTVNWBPCZW
    IWFWCNVNWIWLXDWKOAIVIUTWKWBAVJVEVKVTDNVNVLVMVHT $.
    $( [22-May-2014] $)

  $( Compute the binomial coefficent of ` ( N + 1 ) ` over ` ( N - 1 ) `
     (Contributed by Scott Fenton, 11-May-2014.)  (Revised by Mario Carneiro,
     22-May-2014.) $)
  bcp1m1 $p |- ( N e. NN0 -> ( ( N + 1 ) _C ( N - 1 ) ) =
                 ( ( ( N + 1 ) x. N ) / 2 ) ) $=
    ( cn0 wcel c1 caddc co cmin cbc c2 cmul cdiv cz wceq peano2nn0 peano2zm syl
    nn0z cc ax-1cn oveq2d bccmpl syl2anc nn0cn pnncan syl3anc df-2 syl6eqr bcn2
    a1i pncan sylancl oveq1d eqtrd 3eqtrd ) ABCZADEFZADGFZHFZUPUPUQGFZHFZUPIHFZ
    UPAJFZIKFZUOUPBCZUQLCZURUTMANZUOALCVEAQAOPUQUPUAUBUOUSIUPHUOUSDDEFZIUOARCZD
    RCZVIUSVGMAUCZVIUOSUIZVKADDUDUEUFUGTUOVAUPUPDGFZJFZIKFZVCUOVDVAVNMVFUPUHPUO
    VMVBIKUOVLAUPJUOVHVIVLAMVJSADUJUKTULUMUN $.
    $( [22-May-2014] $) $( [11-May-2014] $)

  $( Pascal's rule for the binomial coefficient, generalized to all integers
     ` K ` .  Equation 2 of [Gleason] p. 295.  (Revised by Mario Carneiro,
     10-Mar-2014.) $)
  bcpasc $p |- ( ( N e. NN0 /\ K e. ZZ ) ->
                ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) ) $=
    ( cn0 wcel cz cc0 c1 caddc co cfz cbc cmin wceq oveq2d cn cdiv cmul syl3anc
    syl cc wa wo peano2nn0 cuz cfv elfzp12 nn0uz eleq2s ax-1cn addid1i bcn0 clt
    wb wbr 0z 1z zsubcl mp2an cr ltm1 ax-mp orci bcval4 mp3an23 oveq12d 3eqtr4a
    0re oveq2 oveq1 eqeq12d syl5ibrcom simpr addid2i syl6eleq nn0p1nn nnuz fzm1
    oveq1i biimpa sylan nn0cn pncan sylancl eleq2d wss 1nn0 eleqtri mpan sselda
    fzss1 bcp1n syldan wne crp bcrpcl rpcn adantr a1i elfzelz adantl zcn addsub
    nncn fznn0sub eqeltrd nnne0 div12 syl112anc nnrp rpdivcl mulcom eqtrd npcan
    syl2anc adddi 3eqtr2d divcan1 elfznn divdiv2 syl122anc subsub eqtr4d oveq1d
    bcm1k fzelp1 nnz elfzm1b syl2anr mpbid eleqtrd rpne0 divmul3 sylan9eqr bcnn
    mpbird imp wn con3d bcval3 cvv div23 3eqtr3rd 3eqtrrd nn0re mpd3an23 jaodan
    ltp1 olcd jaod sylbid adantlr 00id simpll simplr fzssp1 sseld peano2zm nn0z
    ex id ovex fzp1ss eqsstr3i sseli syl6bir sylbird pm2.61dan ) BCDZAEDZUAZAFB
    GHIZJIZDZBAKIZBAGLIZKIZHIZUVKAKIZMZUVHUVMUVSUVIUVHUVMUVSUVHUVMAFMZAFGHIZUVK
    JIZDZUBZUVSUVHUVKCDZUVMUWDUMZBUCZUWFUVKFUDUEZCAFUVKUFUGUHSUVHUVTUVSUWCUVHUV
    SUVTBFKIZBFGLIZKIZHIZUVKFKIZMUVHGFHIGUWLUWMGUIUJUVHUWIGUWKFHBUKUVHUWJEDZUWJ
    FULUNZBUWJULUNZUBUWKFMFEDZGEDUWNUOUPFGUQURUWOUWPFUSDUWOVGFUTVAVBUWJBVCVDVEU
    VHUWEUWMGMUWGUVKUKSVFUVTUVQUWLUVRUWMUVTUVNUWIUVPUWKHAFBKVHUVTUVOUWJBKAFGLVI
    NVEAFUVKKVHVJVKUVHUWCUVSUVHUWCAGUVKJIZDZUVSUVHUWCUAAUWBUWRUVHUWCVLUWAGUVKJG
    UIVMZVRZVNUVHUWSAGUVKGLIZJIZDZAUVKMZUBZUVSUVHUVKGUDUEZDZUWSUXFUVHUVKOUXGBVO
    ZVPVNUXHUWSUXFAGUVKVQVSVTUVHUXDUVSUXEUVHUXDAGBJIZDZUVSUVHUXDUXKUVHUXCUXJAUV
    HUXBBGJUVHBTDZGTDZUXBBMZBWAZUIBGWBZWCZNWDVSUVHUXKUAZUVRUVNUVKUVKALIZPIQIZUV
    NUXSPIZUXSQIZUYAAQIZHIZUVQUVHUXKAFBJIZDZUVRUXTMUVHUXJUYEAGUWHDUVHUXJUYEWEGC
    UWHWFUGWGGFBCWJWHWIZABCWKWLUXRUXTUYAUVKQIZUYAUXSAHIZQIZUYDUXRUXTUVKUYAQIZUY
    HUXRUVNTDZUVKTDZUXSTDZUXSFWMZUXTUYKMUXRUVNWNDZUYLUVHUXKUYFUYPUYGABCWOWLZUVN
    WPSZUXRUVKODZUYMUVHUYSUXKUXIWQUVKXCSZUXRUXSODZUYNUXRUXSBALIZGHIZOUXRUXLUXMA
    TDZUXSVUCMUVHUXLUXKUXOWQZUXMUXRUIWRZUXRUVIVUDUXKUVIUVHAGBWSZWTAXASZBGAXBRZU
    XRVUBCDVUCODCAGBXDVUBVOSXEZUXSXCSZUXRVUAUYOVUJUXSXFSZUVNUVKUXSXGXHUXRUYMUYA
    TDZUYKUYHMUYTUXRUYAWNDZVUMUXRUYPUXSWNDZVUNUYQUXRVUAVUOVUJUXSXISZUVNUXSXJXNU
    YAWPSZUVKUYAXKXNXLUXRUYIUVKUYAQUXRUYMVUDUYIUVKMUYTVUHUVKAXMXNNUXRVUMUYNVUDU
    YJUYDMVUQVUKVUHUYAUXSAXORXPUXRUYBUVNUYCUVPHUXRUYLUYNUYOUYBUVNMUYRVUKVULUVNU
    XSXQRUXRUVNUXSAPIZPIZUVNAQIUXSPIZUVPUYCUXRUYLUYNUYOVUDAFWMZVUSVUTMUYRVUKVUL
    VUHUXRAODZVVAUXKVVBUVHABXRWTZAXFSUVNUXSAXSXTUXRVUSUVPMZUVNUVPVURQIZMZUXRUVN
    UVPBUVOLIZAPIZQIVVEABCYDUXRVVHVURUVPQUXRVVGUXSAPUXRVVGVUCUXSUXRUXLVUDUXMVVG
    VUCMVUEVUHVUFBAGYARVUIYBYCNXLUXRUYLUVPTDZVURTDZVURFWMZVVDVVFUMUYRUXRUVPWNDZ
    VVIUVHUXKUVOUYEDZVVLUXRUVOFUXBJIZUYEUXRUWSUVOVVNDZCAGBYEUXKUVIUVKEDZUWSVVOU
    MZUVHVUGUVHUYSVVPUXIUVKYFSZAUVKYGZYHYIUVHVVNUYEMUXKUVHUXBBFJUXQNWQYJUVOBCWO
    WLUVPWPSUXRVURWNDZVVJUXRVUOAWNDZVVTVUPUXRVVBVWAVVCAXISUXSAXJXNZVURWPSUXRVVT
    VVKVWBVURYKSUVNUVPVURYLXHYOUXRUYLVUDUYNUYOVUTUYCMUYRVUHVUKVULUVNAUXSUUAXHUU
    BVEUUCWLUVHUXEUAZUWAGUVQUVRUWTVWCUVNFUVPGHUXEUVHUVNBUVKKIZFAUVKBKVHUVHVVPUV
    KFULUNZBUVKULUNZUBVWDFMVVRUVHVWFVWEUVHBUSDVWFBUUDBUUGSUUHUVKBVCUUEYMVWCUVPB
    BKIZGVWCUVOBBKUXEUVHUVOUXBBAUVKGLVIUXQYMNUVHVWGGMUXEBYNWQXLVEUXEUVHUVRUVKUV
    KKIZGAUVKUVKKVHUVHUWEVWHGMUWGUVKYNSYMVFUUFWLWLUUSUUIUUJYPUUKUVJUVMYQZUAZFFH
    IFUVQUVRUULVWJUVNFUVPFHVWJUVHUVIUYFYQZUVNFMUVHUVIVWIUUMZUVHUVIVWIUUNZUVJVWI
    VWKUVJUYFUVMUVJUYEUVLAUVHUYEUVLWEUVIFBCUUOWQUUPYRYPABYSRVWJUVHUVOEDZVVMYQZU
    VPFMVWLVWJUVIVWNVWMAUUQSUVJVWIVWOUVJVVMUVMUVJVVMVVOUVMUVJVVNUYEUVOUVJUXBBFJ
    UVJUXLUXMUXNUVHUXLUVIUXOWQUIUXPWCNWDUVJVVOUWSUVMUVIUVIVVPVVQUVHUVIUUTUVHUWE
    VVPUWGUVKUURSVVSYHUWRUVLAUWRUWBUVLUXAUWQUVKYTDUWBUVLWEUOBGHUVAFUVKYTUVBURUV
    CUVDUVEUVFYRYPUVOBYSRVEVWJUWEUVIVWIUVRFMVWJUVHUWEVWLUWGSVWMUVJVWIVLAUVKYSRV
    FUVG $.
    $( [10-Mar-2014] $) $( [13-Jul-2005] $)

  ${
    $d k m n N $.  $d k m n K $.
    $( A binomial coefficient, in its extended domain, is a nonnegative
       integer. $)
    bccl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) e. NN0 ) $=
      ( vk vm cn0 wcel cv cbc co cz wral cc0 c1 wceq oveq1 eleq1d ralbidv oveq2
      wa 0nn0 vn caddc cfz elfz1eq adantl bcn0 1nn0 eqeltri syl6eqel syl bcval3
      ax-mp mp3an1 pm2.61dan rgen cbvralv cmin bcpasc adantlr rcla4cva peano2zm
      wn sylan2 nn0addcl syl2anc adantll eqeltrrd ralrimiva syl5bi nn0ind sylan
      ex ) BEFBCGZHIZEFZCJKZAJFBAHIZEFZDGZVMHIZEFZCJKLVMHIZEFZCJKUAGZVMHIZEFZCJ
      KZWDMUBIZVMHIZEFZCJKZVPDUABVSLNZWAWCCJWLVTWBEVSLVMHOPQVSWDNZWAWFCJWMVTWEE
      VSWDVMHOPQVSWHNZWAWJCJWNVTWIEVSWHVMHOPQVSBNZWAVOCJWOVTVNEVSBVMHOPQWCCJVMJ
      FZVMLLUCIFZWCWPWQSVMLNZWCWQWRWPVMLUDUEWRWBLLHIZEVMLLHRWSMELEFZWSMNTLUFULU
      GUHUIUJWPWQVBZSWBLEWTWPXAWBLNTVMLUKUMTUIUNUOWGWDVSHIZEFZDJKZWDEFZWKWFXCCD
      JVMVSNWEXBEVMVSWDHRPUPXEXDWKXEXDSZWJCJXFWPSWEWDVMMUQIZHIZUBIZWIEXEWPXIWIN
      XDVMWDURUSXDWPXIEFZXEXDWPSWFXHEFZXJXCWFDVMJVSVMNXBWEEVSVMWDHRPUTWPXDXGJFX
      KVMVAXCXKDXGJVSXGNXBXHEVSXGWDHRPUTVCWEXHVDVEVFVGVHVLVIVJVOVRCAJVMANVNVQEV
      MABHRPUTVK $.
      $( [9-Nov-2013] $) $( [10-Jul-2005] $)

    $( A binomial coefficient, in its standard domain, is a natural number.
       (Revised by Mario Carneiro, 10-Mar-2014.) $)
    bccl2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. NN ) $=
      ( wcel cc0 cfz co wa cbc cn0 clt wbr elfz3nn0 elfzelz adantl bccl syl2anc
      cn cz crp bcrpcl rpgt0 syl elnnnn0b sylanbrc ) BCDZAEBFGDZHZBAIGZJDZEUIKL
      ZUIRDUHBJDASDZUJCABMUGULUFAEBNOABPQUHUITDUKABCUAUIUBUCUIUDUE $.
      $( [10-Mar-2014] $) $( [3-Jan-2006] $)
  $}

  $( The number of permutations of ` N - R ` objects from a collection of ` N `
     objects is a natural number.  (Contributed by Jason Orendorff,
     24-Jan-2007.) $)
  permnn $p |- ( ( N e. NN0 /\ R e. ( 0 ... N ) ) ->
               ( ( ! ` N ) / ( ! ` R ) ) e. NN ) $=
    ( cn0 wcel cc0 cfz co wa cfa cfv cn cmin cmul cdiv elfznn0 faccl syl adantl
    cc nncn fznn0sub nnmulcl syl2anc adantr wceq facne0 divcan4 syl3anc eqeltrd
    wne cbc bcval2 bccl2 eqeltrrd nndivtr syl32anc ) BCDZAEBFGDZHZAIJZKDZBALGZI
    JZUTMGZKDZBIJZSDZVDUTNGZKDVFVDNGZKDVFUTNGKDURVAUQURACDZVAABOZAPQZRZUSVCKDZV
    AVEUSVBCDVNCAEBUAVBPQZVMVCUTUBUCUQVGURUQVFKDVGBPVFTQUDUSVHVCKUSVCSDZUTSDZUT
    EUJZVHVCUEUSVNVPVOVCTQURVQUQURVAVQVLUTTQRURVRUQURVJVRVKAUFQRVCUTUGUHVOUIUSB
    AUKGVIKABCULABCUMUNUTVDVFUOUP $.
    $( [24-Jan-2007] $)

