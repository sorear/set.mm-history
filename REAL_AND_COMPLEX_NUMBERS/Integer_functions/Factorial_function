$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Factorial function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ! $. $( Factorial. $)

  $( Extend class notation to include the factorial of nonnegative integers. $)
  cfa $a class ! $.

  $( Define the factorial function on nonnegative integers.  In the literature,
     the factorial function is written as a postscript exclamation point. $)
  df-fac $a |- ! = ( { <. 0 , 1 >. } u. seq 1 ( x. , _I ) ) $.

  $( Value of the factorial function for positive integers.  (Revised by Mario
     Carneiro, 13-Jul-2013.) $)
  facnn $p |- ( N e. NN ->
      ( ! ` N ) = ( seq 1 ( x. , _I ) ` N ) ) $=
    ( cfa cfv cmul cid c1 cseq wceq cn0 cc0 csn cdif cn 0nn0 elexi 1nn cun cres
    dfn2 eqtr3i cop df-fac cuz nnuz reseq2i cz wcel wfn 1z seqfn fnresdm uneq2i
    mp2b eqtr4i fvsnun2 eleq2s ) ABCADEFGZCHAIJKLZMJFIAUQBJINOFMPOBJFUAKZUQQUSU
    QURRZQUBUTUQUSUQFUCCZRZUTUQVAURUQMVAURUDSTUEFUFUGUQVAUHVBUQHUIDEFUJVAUQUKUM
    TULUNUOSUP $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of 0.  (Revised by Mario Carneiro, 13-Jul-2013.) $)
  fac0 $p |- ( ! ` 0 ) = 1 $=
    ( cc0 cn0 cmul cid cseq cfa 0nn0 elexi 1nn cop csn cun cdif cres df-fac cuz
    c1 cn cfv eqtr3i nnuz dfn2 reseq2i cz wcel wfn wceq 1z seqfn fnresdm uneq2i
    mp2b eqtr4i fvsnun1 ) AQBCDQEZFABGHQRIHFAQJKZUOLUPUOBAKMZNZLOURUOUPUOQPSZNZ
    URUOUSUQUORUSUQUAUBTUCQUDUEUOUSUFUTUOUGUHCDQUIUSUOUJULTUKUMUN $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of 1.  (Revised by Mario Carneiro, 13-Jul-2013.) $)
  fac1 $p |- ( ! ` 1 ) = 1 $=
    ( c1 cfa cfv cmul cid cseq cn wcel wceq 1nn facnn ax-mp cz seq1 fvi 3eqtri
    1z ) ABCZADEAFCZAECZAAGHZRSIJAKLAMHSTIQDEANLUATAIJAGOLP $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of a successor.  (Revised by Mario Carneiro,
     13-Jul-2013.) $)
  facp1 $p |- ( N e. NN0 ->
               ( ! ` ( N + 1 ) ) = ( ( ! ` N ) x. ( N + 1 ) ) ) $=
    ( cn0 wcel cn cc0 wceq wo c1 caddc co cfa cfv cmul elnn0 cid cseq facnn cvv
    ax-1cn eqtri peano2nn syl cuz seqp1 nnuz eleq2s oveq1d ovex oveq2i syl6reqr
    ax-mp 3eqtrd addid2i fveq2i fac1 oveq1 fveq2d fveq2 oveq12d oveq12i mulid1i
    fvi fac0 syl6eq 3eqtr4a jaoi sylbi ) ABCADCZAEFZGAHIJZKLZAKLZVJMJZFZANVHVNV
    IVHVKVJMOHPZLZAVOLZVJOLZMJZVMVHVJDCVKVPFAUAVJQUBVPVSFAHUCLDMOHAUDUEUFVHVMVQ
    VJMJVSVHVLVQVJMAQUGVRVJVQMVJRCVRVJFAHIUHVJRVBUKUIUJULVIEHIJZKLZHVKVMWAHKLHV
    THKHSUMZUNUOTVIVJVTKAEHIUPZUQVIVMEKLZVTMJZHVIVLWDVJVTMAEKURWCUSWEHHMJHWDHVT
    HMVCWBUTHSVATVDVEVFVG $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of 2. $)
  fac2 $p |- ( ! ` 2 ) = 2 $=
    ( c2 cfa cfv c1 caddc cmul df-2 fveq2i cn0 wcel wceq 1nn0 facp1 ax-mp 1p1e2
    co fac1 oveq12i 2cn mulid2i eqtri 3eqtri ) ABCDDEPZBCZDBCZUCFPZAAUCBGHDIJUD
    UFKLDMNUFDAFPAUEDUCAFQORASTUAUB $.
    $( [17-Mar-2005] $)

  $( The factorial of 3. $)
  fac3 $p |- ( ! ` 3 ) = 6 $=
    ( c3 cfa cfv c2 c1 caddc co cmul df-3 fveq2i cn0 wcel wceq 2nn0 facp1 ax-mp
    c6 fac2 eqcomi 3eqtri oveq12i 2cn 3cn mulcomi 3t2e6 ) ABCDEFGZBCZDBCZUFHGZQ
    AUFBIJDKLUGUIMNDOPUIDAHGADHGQUHDUFAHRAUFISUADAUBUCUDUETT $.
    $( [17-Mar-2005] $)

  $( Value of the factorial function expressed recursively. $)
  facnn2 $p |- ( N e. NN ->
               ( ! ` N ) = ( ( ! ` ( N - 1 ) ) x. N ) ) $=
    ( cn wcel cc c1 cmin co cn0 wa cfa cfv cmul wceq elnnnn0 caddc facp1 adantl
    ax-1cn npcan adantr mpan2 fveq2d oveq2d 3eqtr3d sylbi ) ABCADCZAEFGZHCZIZAJ
    KZUGJKZALGZMANUIUGEOGZJKZUKUMLGZUJULUHUNUOMUFUGPQUFUNUJMUHUFUMAJUFEDCUMAMRA
    ESUAZUBTUFUOULMUHUFUMAUKLUPUCTUDUE $.
    $( [2-Dec-2004] $)

  ${
    $d j k N $.
    $( Closure of the factorial function. $)
    faccl $p |- ( N e. NN0 -> ( ! ` N ) e. NN ) $=
      ( vj vk cv cfa cfv cn wcel cc0 c1 caddc co wceq fveq2 eleq1d fac0 eqeltri
      1nn cn0 wa cmul facp1 adantl nn0p1nn nnmulcl sylan2 eqeltrd expcom nn0ind
      ) BDZEFZGHIEFZGHCDZEFZGHZUMJKLZEFZGHZAEFZGHBCAUJIMUKULGUJIENOUJUMMUKUNGUJ
      UMENOUJUPMUKUQGUJUPENOUJAMUKUSGUJAENOULJGPRQUOUMSHZURUOUTTUQUNUPUALZGUTUQ
      VAMUOUMUBUCUTUOUPGHVAGHUMUDUNUPUEUFUGUHUI $.
      $( [2-Dec-2004] $)
  $}

  $( The factorial function is nonzero. $)
  facne0 $p |- ( N e. NN0 -> ( ! ` N ) =/= 0 ) $=
    ( cn0 wcel cfa cfv cn cc0 wne faccl nnne0 syl ) ABCADEZFCLGHAILJK $.
    $( [26-Apr-2005] $)

  ${
    $d j M $.  $d j k N $.
    $( A natural number divides the factorial of an equal or larger number. $)
    facdiv $p |- ( ( M e. NN0 /\ N e. NN /\ N <_ M ) ->
                  ( ( ! ` M ) / N ) e. NN ) $=
      ( cn0 wcel cn cle wbr cfa cfv cdiv co cc0 wceq breq2 fveq2 oveq1d imbi12d
      wi eleq1d imbi2d vj vk cv c1 caddc clt wn nngt0 cr 0re nnre ltnle sylancr
      wb mpbid pm2.21d wa cmul peano2nn0 nn0re syl leloe syl2an nnnn0 nn0leltp1
      wo sylan nn0p1nn nnmulcl sylan2 expcom adantl cc wne faccl nncn nn0cn jca
      nnne0 adantr div23 syl3anc sylibrd imim2d com23 sylbird divcan4 syl5ibcom
      eqeltrd oveq2 a1dd jaod sylbid ex com34 com12 imp4d facp1 a2d nn0ind 3imp
      exp4d ) ACDBEDZBAFGZAHIZBJKZEDZXCBUAUCZFGZXHHIZBJKZEDZRZRXCBLFGZLHIZBJKZE
      DZRZRXCBUBUCZFGZXSHIZBJKZEDZRZRXCBXSUDUEKZFGZYEHIZBJKZEDZRZRXCXDXGRZRUAUB
      AXHLMZXMXRXCYLXIXNXLXQXHLBFNYLXKXPEYLXJXOBJXHLHOPSQTXHXSMZXMYDXCYMXIXTXLY
      CXHXSBFNYMXKYBEYMXJYABJXHXSHOPSQTXHYEMZXMYJXCYNXIYFXLYIXHYEBFNYNXKYHEYNXJ
      YGBJXHYEHOPSQTXHAMZXMYKXCYOXIXDXLXGXHABFNYOXKXFEYOXJXEBJXHAHOPSQTXCXNXQXC
      LBUFGZXNUGZBUHXCLUIDBUIDZYPYQUNUJBUKZLBULUMUOUPXSCDZXCYDYJYTXCYDYFYIYTXCY
      DYFUQUQYAYEURKZBJKZEDZYIYTXCYDYFUUCXCYTYDYFUUCRRXCYTYFYDUUCXCYTYFYDUUCRZR
      XCYTUQZYFBYEUFGZBYEMZVFZUUDXCYRYEUIDZYFUUHUNYTYSYTYECDZUUIXSUSZYEUTVABYEV
      BVCUUEUUFUUDUUGUUEUUFXTUUDXCBCDYTXTUUFUNBVDBXSVEVGUUEYDXTUUCUUEYCUUCXTUUE
      YCYBYEURKZEDZUUCYTYCUUMRXCYCYTUUMYTYCYEEDUUMXSVHYBYEVIVJVKVLUUEUUBUULEUUE
      YAVMDZYEVMDZBVMDZBLVNZUQZUUBUULMYTUUNXCYTYAEDZUUNXSVOZYAVPVAVLZYTUUOXCYTU
      UJUUOUUKYEVQVAVLXCUURYTXCUUPUUQBVPZBVSZVRVTYAYEBWAWBSWCWDWEWFUUEUUGUUCYDU
      UEYABURKZBJKZEDUUGUUCUUEUVEYAEUUEUUNUUPUUQUVEYAMUVAXCUUPYTUVBVTXCUUQYTUVC
      VTYABWGWBYTUUSXCUUTVLWIUUGUVEUUBEUUGUVDUUABJBYEYAURWJPSWHWKWLWMWNWOWPWQYT
      YHUUBEYTYGUUABJXSWRPSWCXBWSWTXA $.
      $( [2-May-2005] $)
  $}

  $( No natural number (greater than one) divides the factorial plus one of an
     equal or larger number. $)
  facndiv $p |- ( ( ( M e. NN0 /\ N e. NN ) /\ ( 1 < N /\ N <_ M ) ) ->
                -. ( ( ( ! ` M ) + 1 ) / N ) e. ZZ ) $=
    ( cn0 wcel cn wa c1 clt wbr cle cfa co cdiv cz cmin syl wceq nncn ad2antrr
    cc cfv caddc wn cr recnz sylan ad2ant2lr facdiv 3expa nnz adantrl zsubcl ex
    nnre syl5com cc0 wne faccl peano2cn nnne0 ad2antlr divsubdir syl3anc ax-1cn
    jca pncan2 sylancl oveq1d eqtr3d eleq1d sylibd mtod ) ACDZBEDZFZGBHIZBAJIZF
    ZFZAKUAZGUBLZBMLZNDZGBMLZNDZVNVPWEUCZVMVQVNBUDDVPWFBUNBUEUFUGVSWCWBVTBMLZOL
    ZNDZWEVSWGNDZWCWIVOVQWJVPVOVQFWGEDZWJVMVNVQWKABUHUIWGUJPUKWCWJWIWBWGULUMUOV
    SWHWDNVSWAVTOLZBMLZWHWDVSWATDZVTTDZBTDZBUPUQZFZWMWHQVMWNVNVRVMWOWNVMVTEDWOA
    URVTRPZVTUSPSVMWOVNVRWSSVNWRVMVRVNWPWQBRBUTVEVAWAVTBVBVCVMWMWDQVNVRVMWLGBMV
    MWOGTDWLGQWSVDVTGVFVGVHSVIVJVKVL $.
    $( [3-May-2005] $)

  ${
    $d j k M $.  $d j N $.
    $( Ordering property of factorial. $)
    facwordi $p |- ( ( M e. NN0 /\ N e. NN0 /\ M <_ N ) ->
                  ( ! ` M ) <_ ( ! ` N ) ) $=
      ( cn0 wcel cle wbr cfa cfv wa wi cc0 c1 breq2 anbi2d fveq2 breq2d imbi12d
      wceq cr syl vj vk cv caddc weq nn0le0eq0 biimpa fveq2d fac0 eqeltri leidi
      co 1re syl6eqbr impexp clt wo wb nn0re peano2re leloe syl2an nn0leltp1 cn
      cmul faccl nncn mulid1 3syl nnre nnnn0 nn0ge0 jca nn0p1nn lemul2a mp3anl1
      cc nnge1 syl21anc eqbrtrrd facp1 breqtrrd adantl adantr peano2nn0 syl3anc
      letr mpan2d imim2d com23 sylbird leid biimpcd syl5 a1dd jaod sylbid com13
      ex com4l a2d imp4a syl5bi nn0ind 3impib 3com12 ) BCDZACDZABEFZAGHZBGHZEFZ
      XGXHXIXLXHAUAUCZEFZIZXJXMGHZEFZJXHAKEFZIZXJKGHZEFZJXHAUBUCZEFZIZXJYBGHZEF
      ZJZXHAYBLUDULZEFZIZXJYHGHZEFZJZXHXIIZXLJUAUBBXMKRZXOXSXQYAYOXNXRXHXMKAEMN
      YOXPXTXJEXMKGOPQUAUBUEZXOYDXQYFYPXNYCXHXMYBAEMNYPXPYEXJEXMYBGOPQXMYHRZXOY
      JXQYLYQXNYIXHXMYHAEMNYQXPYKXJEXMYHGOPQXMBRZXOYNXQXLYRXNXIXHXMBAEMNYRXPXKX
      JEXMBGOPQXSXJXTXTEXSAKGXHXRAKRAUFUGUHXTXTLSUIUMUJUKUNYGXHYCYFJZJZYBCDZYMX
      HYCYFUOUUAYTXHYIYLUUAXHYSYIYLJYIUUAXHYSYLXHUUAYIYSYLJZXHUUAYIUUBJXHUUAIZY
      IAYHUPFZAYHRZUQZUUBXHASDYHSDZYIUUFURUUAAUSUUAYBSDUUGYBUSYBUTTZAYHVAVBUUCU
      UDUUBUUEUUCUUDYCUUBAYBVCUUCYSYCYLUUCYFYLYCUUCYFYEYKEFZYLUUAUUIXHUUAYEYEYH
      VEULZYKEUUAYELVEULZYEUUJEUUAYEVDDZYEVQDUUKYERYBVFZYEVGYEVHVIUUAUUGYESDZKY
      EEFZIZLYHEFZUUKUUJEFZUUHUUAUUNUUOUUAUULUUNUUMYEVJTZUUAUULYECDUUOUUMYEVKYE
      VLVIVMUUAYHVDDUUQYBVNYHVRTLSDUUGUUPUUQUURUMLYHYEVOVPVSVTYBWAWBWCUUCXJSDZU
      UNYKSDZYFUUIIYLJXHUUTUUAXHXJVDDUUTAVFXJVJTZWDUUAUUNXHUUSWCUUAUVAXHUUAYHCD
      YKVDDUVAYBWEYHVFYKVJVIWCXJYEYKWGWFWHWIWJWKUUCUUEYLYSXHUUEYLJUUAUUEXJYKRZX
      HYLAYHGOXHUUTXJXJEFZUVCYLJUVBXJWLUVCUVDYLXJYKXJEMWMVIWNWDWOWPWQWSWRWTXAXB
      XCXDXEXF $.
      $( [9-Dec-2005] $)

    $( A lower bound for the factorial function. $)
    faclbnd $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                   ( M ^ ( N + 1 ) ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
      ( wcel cc0 wceq c1 caddc co cexp cfa cfv cmul cle wbr wi oveq2d cr cc syl
      wa vj vk cn0 cn wo elnn0 oveq1 fveq2 breq12d imbi2d cuz nnre nnge1 elnnuz
      cv biimpi expwordi syl3anc ax-1cn addid2i oveq2i a1i fac0 reexpcl syl2anc
      nnnn0 recnd mulid1 syl5eq 3brtr4d clt peano2re lelttric ad3antrrr simpllr
      nn0re syl2an peano2nn0 nngt0 0re ltle mpan sylc expge0 faccl 3syl remulcl
      jca31 jca32 simplr simprr lemul12a anandis nncn expp1 adantr facp1 adantl
      nn0cn peano2cn mulass eqtr4d exp32 com23 nn0ltp1le syl2anr ad2antrr simpr
      jca wb cz ad2antlr nn0z nnz eluz mpbird anim12i id nn0ge0 lemulge11 letrd
      sylbid a1dd jaod mpd expcom a2d nn0ind impcom nn0p1nn 0exp 0cn exp0 ax-mp
      ex oveq1i mulid2 oveq12 anidms oveq1d syl5ibr imp jaoian sylanb ) AUCCZAU
      DCZADEZUEBUCCZABFGHZIHZAAIHZBJKZLHZMNZAUFUUFUUHUUNUUGUUHUUFUUNUUFAUAUOZFG
      HZIHZUUKUUOJKZLHZMNZOUUFADFGHZIHZUUKDJKZLHZMNZOUUFAUBUOZFGHZIHZUUKUVFJKZL
      HZMNZOUUFAUVGFGHZIHZUUKUVGJKZLHZMNZOUUFUUNOUAUBBUUODEZUUTUVEUUFUVQUUQUVBU
      USUVDMUVQUUPUVAAIUUODFGUGPUVQUURUVCUUKLUUODJUHPUIUJUUOUVFEZUUTUVKUUFUVRUU
      QUVHUUSUVJMUVRUUPUVGAIUUOUVFFGUGPUVRUURUVIUUKLUUOUVFJUHPUIUJUUOUVGEZUUTUV
      PUUFUVSUUQUVMUUSUVOMUVSUUPUVLAIUUOUVGFGUGPUVSUURUVNUUKLUUOUVGJUHPUIUJUUOB
      EZUUTUUNUUFUVTUUQUUJUUSUUMMUVTUUPUUIAIUUOBFGUGPUVTUURUULUUKLUUOBJUHPUIUJU
      UFAFIHZUUKUVBUVDMUUFAQCZFAMNZAFUKKCZUWAUUKMNAULZAUMZUUFUWDAUNUPAFAUQURUVB
      UWAEUUFUVAFAIFUSUTVAVBUUFUVDUUKFLHZUUKUVCFUUKLVCVAUUFUUKRCZUWGUUKEUUFUUKU
      UFUWBUUEUUKQCZUWEAVFZAAVDZVEZVGZUUKVHSVIVJUVFUCCZUUFUVKUVPUUFUWNUVKUVPOZU
      UFUWNTZAUVGMNZUVGAVKNZUEZUWOUUFUWBUVGQCZUWSUWNUWEUWNUVFQCZUWTUVFVPZUVFVLZ
      SAUVGVMVQUWPUWQUWOUWRUWPUVKUWQUVPUWPUVKUWQUVPUWPUVKUWQTZTUVHALHZUVJUVGLHZ
      UVMUVOMUWPUVKUWQUXEUXFMNZUWPUVKTZUWPUWQTZTZUVHQCZDUVHMNZTUVJQCZTZUWBDAMNZ
      TZUWTTTUXDUXGUXJUXNUXPUWTUXJUXKUXLUXMUXJUWBUVGUCCZUXKUUFUWBUWNUVKUXIUWEVN
      ZUXJUWNUXQUUFUWNUVKUXIVOZUVFVRZSZAUVGVDVEUXJUWBUXQUXOUXLUXRUYAUXJUWBDAVKN
      ZUXOUXRUUFUYBUWNUVKUXIAVSVNDQCUWBUYBUXOOVTDAWAWBWCZAUVGWDURUXJUWIUVIQCZUX
      MUXJUWBUUEUWIUXRUUFUUEUWNUVKUXIUWJVNUWKVEUXJUWNUVIUDCZUYDUXSUVFWEZUVIULZW
      FUUKUVIWGVEWHUXJUWBUXOUXRUYCXIUXJUWNUXAUWTUXSUXBUXCWFWIUXJUVKUWQUWPUVKUXI
      WJUXHUWPUWQWKXIUVHUVJAUVGWLWCWMUWPUVMUXEEZUXDUUFARCUXQUYHUWNAWNUXTAUVGWOV
      QWPUWPUVOUXFEUXDUWPUVOUUKUVIUVGLHZLHZUXFUWPUVNUYIUUKLUWNUVNUYIEUUFUVFWQWR
      PUWPUWHUVIRCZUVGRCZUXFUYJEUUFUWHUWNUWMWPUWNUYKUUFUWNUVIUWNUYEUYDUYFUYGSVG
      WRUWNUYLUUFUWNUVFRCUYLUVFWSUVFWTSWRUUKUVIUVGXAURXBWPVJXCXDUWPUWRUVPUVKUWP
      UWRUVLAMNZUVPUWNUXQUUEUWRUYMXJUUFUXTUWJUVGAXEXFUWPUYMUVPUWPUYMTZUVMUUKUVO
      UWPUVMQCZUYMUUFUWBUVLUCCZUYOUWNUWEUWNUXQUYPUXTUVGVRSZAUVLVDVQWPUUFUWIUWNU
      YMUWLXGUWPUVOQCZUYMUUFUWIUVNQCZUYRUWNUWLUWNUXQUVNUDCZUYSUXTUVGWEZUVNULWFZ
      UUKUVNWGVQWPUYNUWBUWCAUVLUKKCZUVMUUKMNUUFUWBUWNUYMUWEXGUUFUWCUWNUYMUWFXGU
      YNVUCUYMUWPUYMXHUYNUVLXKCZAXKCZVUCUYMXJUYNUYPVUDUWNUYPUUFUYMUYQXLUVLXMSUU
      FVUEUWNUYMAXNXGUVLAXOVEXPAUVLAUQURUWPUUKUVOMNZUYMUWPUWIUYSTDUUKMNZFUVNMNZ
      TVUFUUFUWIUWNUYSUWLVUBXQUUFVUGUWNVUHUUFUUEVUGUWJUUEUWBUUEUXOVUGAVPUUEXRAX
      SAAWDURSUWNUXQUYTVUHUXTVUAUVNUMWFXQUUKUVNXTVEWPYAYOYBYCYDYEYFYGYHYIUUGUUH
      UUNUUHUUNUUGDUUIIHZDDIHZUULLHZMNUUHDUULVUIVUKMUUHUULUDCZUULUCCDUULMNBWEZU
      ULVFUULXSWFUUHUUIUDCVUIDEBYJUUIYKSUUHVUKFUULLHZUULVUJFUULLDRCVUJFEYLDYMYN
      YPUUHVULUULRCVUNUULEVUMUULWNUULYQWFVIVJUUGUUJVUIUUMVUKMADUUIIUGUUGUUKVUJU
      ULLUUGUUKVUJEADADIYRYSYTUIUUAUUBUUCUUD $.
      $( [17-Dec-2005] $)
  $}

  $( A lower bound for the factorial function. $)
  faclbnd2 $p |- ( N e. NN0 -> ( ( 2 ^ N ) / 2 ) <_ ( ! ` N ) ) $=
    ( cn0 wcel c2 cexp co cdiv c1 cle cmul c4 sq2 cc wceq 2cn mpan wa cc0 wbr
    cr caddc cfa cfv 2t2e4 eqtr4i oveq2i expp1 oveq1d syl5eq wne 2ne0 divmuldiv
    expcl pm3.2i mpanr12 sylancl dividi halfcl mulid1 3syl 3eqtr2rd faclbnd 2re
    2nn0 wb peano2nn0 reexpcl sylancr cn nnre syl clt 4re eqeltri 4pos breqtrri
    faccl ledivmul mp3an3 syl2anc mpbird eqbrtrd ) ABCZDAEFZDGFZDAHUAFZEFZDDEFZ
    GFZAUBUCZIWCWIWDDJFZDDJFZGFZWEDDGFZJFZWEWCWIWGWLGFWMWHWLWGGWHKWLLUDUEUFWCWG
    WKWLGDMCZWCWGWKNODAUGPUHUIWCWDMCZWPWOWMNZWPWCWQODAUMPZOWQWPQWPDRUJZQZXAWRWP
    WTOUKUNZXBWDDDDULUOUPWCWOWEHJFZWEWNHWEJDOUKUQUFWCWQWEMCXCWENWSWDURWEUSUTUIV
    AWCWIWJISZWGWHWJJFISZDBCWCXEVDDAVBPWCWGTCZWJTCZXDXEVEZWCDTCWFBCXFVCAVFDWFVG
    VHWCWJVICXGAVQWJVJVKXFXGWHTCZRWHVLSZQXHXIXJWHKTLVMVNRKWHVLVOLVPUNWGWJWHVRVS
    VTWAWB $.
    $( [17-Dec-2005] $)

  $( A lower bound for the factorial function. $)
  faclbnd3 $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                 ( M ^ N ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cn cc0 wceq wo cexp co cle wbr wa c1 cr adantr sylan reexpcl 1re
    cmul cfa cfv elnn0 caddc cuz nnre nnge1 cz nn0z uzid peano2uz 3syl expwordi
    adantl syl3anc nnnn0 faclbnd wi nn0re peano2nn0 syl2an mpancom remulcl letr
    faccl syl mp2and 0exp 0re lt01 ltleii syl6eqbr oveq2 0cn exp0 ax-mp eqbrtri
    cc leidi jaoi sylbi 1nn nnmulcl sylancr mpan mp3an2 syl2anc wb oveq1 oveq12
    anidms syl6eq oveq1d breq12d mpbird jaoian sylanb ) ACDZAEDZAFGZHBCDZABIJZA
    AIJZBUAUBZTJZKLZAUCWSXAXFWTWSXAMZXBABNUDJZIJZKLZXIXEKLZXFXGAODZNAKLZXHBUEUB
    ZDZXJWSXLXAAUFPWSXMXAAUGPXGBUHDZBXNDXOXAXPWSBUIUNBUJBBUKULABXHUMUOWSWRXAXKA
    UPZABUQQWSWRXAXJXKMXFURZXQWRXAMXBODZXIODZXEODZXRWRXLXAXSAUSZABRQWRXLXHCDXTX
    AYBBUTAXHRVAWRXCODZXDODZYAXAXLWRYCYBAARVBXAXDEDZYDBVEZXDUFVFZXCXDVCVAXBXIXE
    VDUOQVGWTXAMXFFBIJZNXDTJZKLZXAYJWTXAYHNKLZNYIKLZYJXABEDZBFGZHYKBUCYMYKYNYMY
    HFNKBVHFNVISVJVKVLYNYHFFIJZNKBFFIVMYONNKFVRDYONGVNFVOVPZNSVSVQVLVTWAXAYIEDZ
    YLXANEDYEYQWBYFNXDWCWDYIUGVFXAYHODZYIODZYKYLMYJURZFODXAYRVIFBRWEXANODZYDYSS
    YGNXDVCWDYRUUAYSYTSYHNYIVDWFWGVGUNWTXFYJWHXAWTXBYHXEYIKAFBIWIWTXCNXDTWTXCYO
    NWTXCYOGAFAFIWJWKYPWLWMWNPWOWPWQ $.
    $( [19-Dec-2005] $)

  ${
    faclbnd4lem1.1 $e |- N e. NN $.
    faclbnd4lem1.2 $e |- K e. NN0 $.
    faclbnd4lem1.3 $e |- M e. NN0 $.
    $( Lemma for ~ faclbnd4 .  Prepare the induction step. $)
    faclbnd4lem1 $p |- ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) $=
      ( c1 cle wbr co cexp cmul c2 cr wcel mp2an cc0 wa cn0 remulcli cmin caddc
      clt wo cfa cfv nnrei 1re lelttric wceq letri3i cn nnge1 mpbiran2 0re lt01
      ax-mp ltleii pm3.2i 2re 1nn nn0nnaddcl nnnn0i 2nn0 reexpcl nn0rei nn0ge0i
      nn0expcl cc 2cn exp0 cuz 1lt2 nn0uz eleqtri expwordi mp3an eqbrtrri elnn0
      nncn exp1 syl nnuz mp3an13 eqbrtrrd breq1 mpbiri sylbi lemul12a mp2 oveq1
      jaoi nnzi 1exp syl6eq oveq2 nn0cni oveq12d fveq2 fac1 oveq2d recni mulcli
      cz mulid1i breq12d sylbir adantr a1i faccl nn0mulcli nncni expm1t oveq12i
      expp1 elnnnn0 mpbi simpri mulassi eqtri nn0addcli w3a 3pm3.2i wb nnltp1le
      df-2 breq1i bitr4i expubnd mp3an12 lemul1a mul4i oveq2i mpan letrd ax-1cn
      syl6breq eqtr3i nn0zi sqvali sylancr mul12i 3eqtr2i adantl mul32i addassi
      facnn2 3eqtr4i syl5eqbr expadd eluz1i ltp1i eqcomi adddii eqtr4i mpbir2an
      3brtr4i syl6breqr jaoian ) CGHIZGCUCIZUDZCGUAJZAKJZBUVCKJZLJZMAMKJZKJZBBA
      UBJZKJZLJZUVCUEUFZLJZHIZCAGUBJZKJZBCKJZLJZMUVOMKJZKJZBBUVOUBJZKJZLJZCUEUF
      ZLJZHIZCNOZGNOZUVBCDUGZUHCGUIPUUTUVNUWFUVAUUTUWFUVNUUTCGUJZUWFUWJUUTGCHIZ
      CGUWIUHUKCULOZUWKDCUMUQUNUWJUWFGBLJZUWCHIZUWHQGHIZRZUVTNOZRZBNOZQBHIZRZUW
      BNOZRZRGUVTHIZBUWBHIZRUWNUWRUXCUWPUWQUWHUWOUHQGUOUHUPURUSMNOZUVSSOZUWQUTU
      VOSOZMSOZUXGUVOASOZGULOZUVOULOZEVAAGVBPZVCZVDUVOMVHPZMUVSVEPZUSUXAUXBUWSU
      WTBFVFZBFVGUSUWBBSOZUWASOUWBSOFUWAUXRUXLUWAULOFUXMBUVOVBPZVCBUWAVHPZVFUSU
      SUXDUXEMQKJZGUVTHMVIOZUYAGUJVJMVKUQUXFGMHIZUVSQVLUFZOUYAUVTHIUTGMUHUTVMUR
      ZUVSSUYDUXOVNVOMQUVSVPVQVRUXRUXEFUXRBULOZBQUJZUDUXEBVSUYFUXEUYGUYFBGKJZBU
      WBHUYFBVIOZUYHBUJZBVTBWAZWBUYFGBHIZUYHUWBHIZBUMUWSUYLUWAGVLUFZOUYMUXQUWAU
      LUYNUXSWCVOBGUWAVPWDWBWEUYGUXEQUWBHIUWBUXTVGBQUWBHWFWGWLWHUQUSGUVTBUWBWIW
      JUWJUVRUWMUWEUWCHUWJUVPGUVQBLUWJUVPGUVOKJZGCGUVOKWKUVOXDOUYOGUJUVOUXMWMUV
      OWNUQWOUWJUVQUYHBCGBKWPUYIUYJBFWQZUYKUQWOWRUWJUWEUWCGLJUWCUWJUWDGUWCLUWJU
      WDGUEUFGCGUEWSWTWOXAUWCUVTUWBUVTUXPXBZUWBUXTWQZXCXEWOXFWGXGXHUVAUVNRZUVRU
      VTUWBUWDLJZLJZUWEHUYSUVRUVHMAKJZLJZUYTLJZVUAUVRNOUYSUVPUVQUWGUXHUVPNOUWIU
      XNCUVOVEPUWSCSOZUVQNOUXQCDVCZBCVEPTXIVUDNOUYSVUCUYTUVHVUBUXFUVGSOZUVHNOUT
      UXJUXIVUGEVDAMVHPZMUVGVEPZVUBUXIUXJVUBSOVDEMAVHPZVFZTZUYTUWBUWDUXTUWDVUEU
      WDULOVUFCXJUQZVCXKZVFZTXIVUANOUYSUVTUYTUXPVUOTXIUYSUVRCAKJZCUVEBLJZLJZLJZ
      VUDHUVRVUPCLJZVUQLJVUSUVPVUTUVQVUQLCVIOZUXJUVPVUTUJCDXLZECAXOPUYIUWLUVQVU
      QUJUYPDBCXMPXNVUPCVUQVUPUWGUXJVUPNOZUWIECAVEPZXBVVBVUQUVEBUXRUVCSOZUVESOF
      VVAVVEUWLVVAVVERDCXPXQXRZBUVCVHPZFXKZWQZXSXTUYSVUSUVMVUBCLJZBLJZLJZVUDHUY
      SVUSUVFVVKLJZVVLVUSNOUYSVUPVURVVDVURCVUQVUFVVHXKZVFZTXIVVMNOUYSUVFVVKUVDU
      VEUVCNOUXJUVDNOUVCVVFVFEUVCAVEPZUVEVVGVFTZVVKVVJBVUBCVUJVUFXKZFXKZVFZTXIV
      VLNOUYSUVMVVKUVKUVLUVHUVJVUIUWSUVISOZUVJNOUXQBAFEYAZBUVIVEPZTZUVLVVEUVLUL
      OVVFUVCXJUQZUGTZVVTTXIUVAVUSVVMHIUVNUVAVUSVUBUVDLJZVURLJZVVMHUVAVVCVWGNOZ
      VURNOZQVURHIZRZYBVUPVWGHIZVUSVWHHIVVCVWIVWLVVDVUBUVDVUKVVPTVWJVWKVVOVURVV
      NVGUSYCUVAMCHIZVWMUVAGGUBJZCHIZVWNUXKUWLUVAVWPYDVADGCYEPMVWOCHYFYGYHUWGUX
      JVWNVWMUWIECAYIYJWHVUPVWGVURYKUUAVWHVVJUVDVUQLJZLJVVJUVFBLJZLJVVMVUBUVDCV
      UQVUBVUJWQZUVDVVPXBZVVBVVIYLVWRVWQVVJLUVDUVEBVWTUVEVVGWQUYPXSYMVVJUVFBVVJ
      VVRWQUVFVVQXBUYPUUBUUCYQXHUVNVVMVVLHIZUVAUVFNOZUVMNOZVVKNOZQVVKHIZRZYBUVN
      VXAVXBVXCVXFVVQVWFVXDVXEVVTVVKVVSVGUSYCUVFUVMVVKYKYNUUDYOVVLVUCUWBLJZUWDL
      JZVUDUVMVUBBLJZCLJZLJUVKVXILJZUVLCLJZLJVVLVXHUVKUVLVXICUVKVWDXBUVLVWEXLVU
      BBVWSUYPXCVVBYLVVKVXJUVMLVUBCBVWSVVBUYPUUEYMVXGVXKUWDVXLLVUCUVJBLJZLJVXGV
      XKVXMUWBVUCLBUVIGUBJZKJZVXMUWBUYIVWAVXOVXMUJUYPVWBBUVIXOPVXNUWABKBAGUYPAE
      WQZYPUUFYMYRYMUVHVUBUVJBUVHVUIXBVWSUVJVWCXBUYPYLYRUWLUWDVXLUJDCUUGUQXNUUH
      VUCUWBUWDVUCVULXBUYRUWDVUMXLZXSXTYQUUIVUDVUAHIZUYSVUCNOZUWQUYTNOZQUYTHIZR
      ZYBVUCUVTHIVXRVXSUWQVYBVULUXPVXTVYAVUOUYTVUNVGUSYCMUVGAUBJZKJZVUCUVTHUYBV
      UGUXJVYDVUCUJVJVUHEMUVGAUUJVQUXFUYCUVSVYCVLUFOZVYDUVTHIUTUYEVYEUVSXDOVYCU
      VSHIVYCUVSVYCUVGAVUHEYAYSUUKUVSUXOYSAUVOLJZUVOUVOLJZVYCUVSHANOZUVONOZVYIQ
      UVOHIZRZYBAUVOHIVYFVYGHIVYHVYIVYKAEVFZUVOUXMUGZVYIVYJVYMUVOUXNVGUSYCAUVOV
      YLVYMAVYLUULURAUVOUVOYKPVYCAALJZAGLJZUBJVYFUVGVYNAVYOUBAVXPYTVYOAAVXPXEUU
      MXNAAGVXPVXPYPUUNUUOUVOUVOUXMXLYTUUQUUPMVYCUVSVPVQVRVUCUVTUYTYKPXIYOUVTUW
      BUWDUYQUYRVXQXSUURUUSYN $.
      $( [20-Dec-2005] $)
  $}

  $( Lemma for ~ faclbnd4 .  Use the weak deduction theorem to convert the
     hypotheses of ~ faclbnd4lem1 to antecedents. $)
  faclbnd4lem2 $p |- ( ( M e. NN0 /\ K e. NN0 /\ N e. NN ) ->
           ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) ) $=
    ( cn0 c1 co cexp cmul c2 caddc cfa cfv cle wbr oveq1 oveq2d oveq12d breq12d
    wi oveq1d wcel cn cmin cif wceq imbi12d oveq2 fveq2d fveq2 1nn faclbnd4lem1
    id elimel 1nn0 dedth3h ) BDUAZADUAZCUBUAZCEUCFZAGFZBUSGFZHFZIAIGFZGFZBBAJFZ
    GFZHFZUSKLZHFZMNZCAEJFZGFZBCGFZHFZIVKIGFZGFZBBVKJFZGFZHFZCKLZHFZMNZSUTUPBEU
    DZUSGFZHFZVDWCWCAJFZGFZHFZVHHFZMNZVLWCCGFZHFZVPWCWCVKJFZGFZHFZVTHFZMNZSUSUQ
    AEUDZGFZWDHFZIWRIGFZGFZWCWCWRJFZGFZHFZVHHFZMNZCWREJFZGFZWKHFZIXHIGFZGFZWCWC
    XHJFZGFZHFZVTHFZMNZSURCEUDZEUCFZWRGFZWCXSGFZHFZXEXSKLZHFZMNZXRXHGFZWCXRGFZH
    FZXOXRKLZHFZMNZSBACEEEBWCUEZVJWJWBWQYLVBWEVIWIMYLVAWDUTHBWCUSGOPYLVGWHVHHYL
    VFWGVDHYLBWCVEWFGYLULZBWCAJOQPTRYLVNWLWAWPMYLVMWKVLHBWCCGOPYLVSWOVTHYLVRWNV
    PHYLBWCVQWMGYMBWCVKJOQPTRUFAWRUEZWJXGWQXQYNWEWTWIXFMYNUTWSWDHAWRUSGUGTYNWHX
    EVHHYNVDXBWGXDHYNVCXAIGAWRIGOPYNWFXCWCGAWRWCJUGPQTRYNWLXJWPXPMYNVLXIWKHYNVK
    XHCGAWREJOZPTYNWOXOVTHYNVPXLWNXNHYNVOXKIGYNVKXHIGYOTPYNWMXMWCGYNVKXHWCJYOPP
    QTRUFCXRUEZXGYEXQYKYPWTYBXFYDMYPWSXTWDYAHYPUSXSWRGCXREUCOZTYPUSXSWCGYQPQYPV
    HYCXEHYPUSXSKYQUHPRYPXJYHXPYJMYPXIYFWKYGHCXRXHGOCXRWCGUGQYPVTYIXOHCXRKUIPRU
    FWRWCXRCEUBUJUMAEDUNUMBEDUNUMUKUO $.
    $( [23-Dec-2005] $)


  $( Lemma for ~ faclbnd4 .  The ` N = 0 ` case. $)
  faclbnd4lem3 $p |- ( ( ( M e. NN0
            /\ K e. NN0 ) /\ N = 0 ) -> ( ( N ^ K ) x. ( M ^ N ) )
       <_ ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa cc0 wceq cexp co cmul c2 caddc cle wbr c1 cn adantl syl6eq cc
    cfa cfv wo elnn0 0exp nnnn0 nn0expcl mpan2 sylancr nn0addcl syldan nn0mulcl
    2nn0 syl2anc sylan2 nn0ge0 eqbrtrd 0nn0 sylancl nnexpcl mpdan id oveq1 00id
    syl oveq12d 0cn exp0 ax-mp 1nn syl6eqel jaoi sylbi nnmulcl mpan 3syl adantr
    nnge1 wb oveq2 sq0i oveq2d 2cn breq12d mpbird jaodan sylan2b nn0cn sylan9eq
    mulid1 3brtr4d fveq2 fac0 ) BDEZADEZFZCGHZFCAIJZBCIJZKJZLALIJZIJZBBAMJZIJZK
    JZCUAUBZKJZNOZGAIJZBGIJZKJZXEPKJZNOZWPXMWQWPXIXEXKXLNWOWNAQEZAGHZUCXIXENOZA
    UDWNXNXPXOWNXNFZXIGXENXNXIGHWNAUERXQXEDEZGXENOXNWNWOXRAUFWPXBDEZXDDEZXRWOXS
    WNWOLDEZXADEZXSUMWOYAYBUMALUGUHLXAUGUIRWNWOXCDEXTBAUJBXCUGUKXBXDULUNZUOXEUP
    VEUQWNXOFXPPPBBGMJZIJZKJZNOZWNYGXOWNYEQEZYFQEZYGWNBQEZBGHZUCYHBUDYJYHYKYJYD
    DEZYHYJWNGDEZYLBUFURBGUJUSBYDUTVAYKYEPQYKYEGGIJZPYKBGYDGIYKVBYKYDGGMJGBGGMV
    CVDSVFGTEYNPHVGGVHVIZSVJVKVLVMPQEYHYIVJPYEVNVOYFVRVPVQXOXPYGVSWNXOXIPXEYFNX
    OXIYNPAGGIVTYOSXOXBPXDYEKXOXBLGIJZPXOXAGLIAWAWBLTEYPPHWCLVHVISXOXCYDBIAGBMV
    TWBVFWDRWEWFWGWNWOXKXIPKJZXIWNXJPXIKWNBTEXJPHBWHBVHVEWBWOXIDEZXITEYQXIHYMWO
    YRURGAUGVOXIWHXIWJVPWIWPXRXETEXLXEHYCXEWHXEWJVPWKVQWQXHXMVSWPWQWTXKXGXLNWQW
    RXIWSXJKCGAIVCCGBIVTVFWQXFPXEKWQXFGUAUBPCGUAWLWMSWBWDRWE $.
    $( [23-Dec-2005] $)

  ${
    $d j m n M $.  $d j m n K $.  $d j n N $.
    $( Lemma for ~ faclbnd4 .  Prove the ` 0 < N ` case by induction on
       ` K ` . $)
    faclbnd4lem4 $p |- ( ( N e. NN /\ K e. NN0 /\ M e. NN0 ) ->
                   ( ( N ^ K ) x. ( M ^ N ) ) <_
              ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
      ( vn cn wcel cexp co cmul c2 caddc cle wbr wa c1 wceq oveq2 oveq2d oveq1d
      cc0 vm vj cn0 cfa cfv cv wral wi oveq1 oveq12d fveq2 breq12d cbvralv cmin
      clt wo cr nnre 1re lelttric sylancl ancli andi sylib nnge1 adantr biimpar
      letri3 anassrs mpdan ax-1cn subidi syl6eq syl faclbnd4lem3 sylan2 a1d 1nn
      nnsub mpan biimpa rcla4v adantl jaodan faclbnd4lem2 syld ralrimdva syl5bi
      wb 3expa expcom a2d nnnn0 faclbnd3 cc nncn exp0 nn0cn expcl syl2an mulid2
      eqtrd sq0 oveq2i 2cn ax-mp eqtri addid1 mpancom 3brtr4d ralrimiva ralbidv
      a1i imbi2d nn0indALT imp rcla4va 3impb ) CEFZAUCFZBUCFZCAGHZBCGHZIHZJAJGH
      ZGHZBBAKHZGHZIHZCUDUEZIHZLMZXTYANXSDUFZAGHZBYMGHZIHZYIYMUDUEZIHZLMZDEUGZY
      LXTYAYTYAYMUAUFZGHZYOIHZJUUAJGHZGHZBBUUAKHZGHZIHZYQIHZLMZDEUGZUHYAYMTGHZY
      OIHZJTJGHZGHZBBTKHZGHZIHZYQIHZLMZDEUGZUHYAYMUBUFZGHZYOIHZJUVBJGHZGHZBBUVB
      KHZGHZIHZYQIHZLMZDEUGZUHYAYMUVBOKHZGHZYOIHZJUVMJGHZGHZBBUVMKHZGHZIHZYQIHZ
      LMZDEUGZUHYAYTUHUAUBAUVBUCFZYAUVLUWCYAUWDUVLUWCUHUVLUUAUVBGHZBUUAGHZIHZUV
      IUUAUDUEZIHZLMZUAEUGZYAUWDNZUWCUVKUWJDUAEYMUUAPZUVDUWGUVJUWILUWMUVCUWEYOU
      WFIYMUUAUVBGUIYMUUABGQUJUWMYQUWHUVIIYMUUAUDUKRULUMUWLUWKUWBDEUWLYMEFZNUWK
      YMOUNHZUVBGHZBUWOGHZIHZUVIUWOUDUEZIHZLMZUWBUWNUWLUWNYMOLMZNZUWNOYMUOMZNZU
      PZUWKUXAUHZUWNUWNUXBUXDUPZNUXFUWNUXHUWNYMUQFZOUQFZUXHYMURZUSYMOUTVAVBUWNU
      XBUXDVCVDUWLUXCUXGUXEUWLUXCNUXAUWKUXCUWLUWOTPZUXAUXCYMOPZUXLUXCOYMLMZUXMU
      WNUXNUXBYMVEVFUWNUXBUXNUXMUWNUXMUXBUXNNZUWNUXIUXJUXMUXOWIUXKUSYMOVHVAVGVI
      VJUXMUWOOOUNHTYMOOUNUIOVKVLVMVNUVBBUWOVOVPVQUXEUXGUWLUXEUWOEFZUXGUWNUXDUX
      POEFUWNUXDUXPWIVROYMVSVTWAUWJUXAUAUWOEUUAUWOPZUWGUWRUWIUWTLUXQUWEUWPUWFUW
      QIUUAUWOUVBGUIUUAUWOBGQUJUXQUWHUWSUVIIUUAUWOUDUKRULWBVNWCWDVPYAUWDUWNUXAU
      WBUHUVBBYMWEWJWFWGWHWKWLYAUUTDEYAUWNNZYOBBGHZYQIHZUUMUUSLUWNYAYMUCFZYOUXT
      LMYMWMZBYMWNVPUXRUUMOYOIHZYOUWNUUMUYCPYAUWNUULOYOIUWNYMWOFUULOPYMWPYMWQVN
      SWCUXRYOWOFZUYCYOPYABWOFZUYAUYDUWNBWRZUYBBYMWSWTYOXAVNXBYAUUSUXTPUWNYAUUR
      UXSYQIYAUUROUXSIHZUXSYAUUOOUUQUXSIUUOOPYAUUOJTGHZOUUNTJGXCXDJWOFUYHOPXEJW
      QXFXGXMYAUUPBBGYAUYEUUPBPUYFBXHVNRUJYAUXSWOFZUYGUXSPUYEYAUYIUYFBBWSXIUXSX
      AVNXBSVFXJXKUUATPZUUKUVAYAUYJUUJUUTDEUYJUUCUUMUUIUUSLUYJUUBUULYOIUUATYMGQ
      SUYJUUHUURYQIUYJUUEUUOUUGUUQIUYJUUDUUNJGUUATJGUIRUYJUUFUUPBGUUATBKQRUJSUL
      XLXNUUAUVBPZUUKUVLYAUYKUUJUVKDEUYKUUCUVDUUIUVJLUYKUUBUVCYOIUUAUVBYMGQSUYK
      UUHUVIYQIUYKUUEUVFUUGUVHIUYKUUDUVEJGUUAUVBJGUIRUYKUUFUVGBGUUAUVBBKQRUJSUL
      XLXNUUAUVMPZUUKUWCYAUYLUUJUWBDEUYLUUCUVOUUIUWALUYLUUBUVNYOIUUAUVMYMGQSUYL
      UUHUVTYQIUYLUUEUVQUUGUVSIUYLUUDUVPJGUUAUVMJGUIRUYLUUFUVRBGUUAUVMBKQRUJSUL
      XLXNUUAAPZUUKYTYAUYMUUJYSDEUYMUUCYPUUIYRLUYMUUBYNYOIUUAAYMGQSUYMUUHYIYQIU
      YMUUEYFUUGYHIUYMUUDYEJGUUAAJGUIRUYMUUFYGBGUUAABKQRUJSULXLXNXOXPYSYLDCEYMC
      PZYPYDYRYKLUYNYNYBYOYCIYMCAGUIYMCBGQUJUYNYQYJYIIYMCUDUKRULXQVPXR $.
      $( [19-Dec-2005] $)
  $}

  $( Variant of ~ faclbnd5 providing a non-strict lower bound. $)
  faclbnd4 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN0 ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <_
            ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cexp co cmul c2 caddc cfa cfv cle wbr wa cn cc0 wceq wo 3com13
    elnn0 faclbnd4lem4 3expa faclbnd4lem3 jaodan sylan2b 3impa ) BDEZADEZCDEZCA
    FGBCFGHGIAIFGFGBBAJGFGHGCKLHGMNZUHUIUJUKUJUHUIOZCPEZCQRZSUKCUAULUMUKUNUHUIU
    MUKUMUIUHUKABCUBTUCABCUDUEUFUGT $.
    $( [23-Dec-2005] $)

  $( The factorial function grows faster than powers and exponentiations.  If
     we consider ` K ` and ` M ` to be constants, the right-hand side of the
     inequality is a constant times ` N ` -factorial. $)
  faclbnd5 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <
      ( ( 2 x. ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cn cexp co cmul c2 clt wbr wa cr reexpcl nnre remulcl syl2an syl
    cc caddc cfa cfv nn0re sylan ancoms anandirs 2nn 2nn0 mpan2 nnexpcl sylancr
    nn0expcl nnnn0 nn0addcl sylan2 anabss7 nnmulcl anabss5 2re faclbnd4 syl3an3
    faccl cle 3coml 3expa c1 1lt2 cc0 wb nngt0 ltmulgt12 mp3an2 syl2anc lelttrd
    mpbii wceq recnd 2cn mulass mp3an1 breqtrrd 3impa 3comr ) ADEZBFEZCDEZCAGHZ
    BCGHZIHZJJAJGHZGHZBBAUAHZGHZIHZIHCUBUCZIHZKLZWEWFWGWRWEWFMZWGMZWJJWOWPIHZIH
    ZWQKWTWJXAXBWEWFWGWJNEZWEWGMWHNEZWINEZXCWFWGMWGWEXDWGCNEWEXDCUDCAOUEUFWFBNE
    WGXEBPBCOUEWHWIQRUGWSWONEZWPNEZXANEZWGWSWOFEZXFWEWFXIWEWLFEZWNFEZXIWSWEJFEW
    KDEZXJUHWEJDEXLUIAJUMUJJWKUKULWEWFXKWSWFWMDEZXKWFWEBDEZXMBUNZXNWEXMBAUOUFUP
    BWMUKUPUQWLWNURRUSZWOPSZWGWPFEZXGCVCZWPPSZWOWPQRZWTJNEZXHXBNEUTYAJXAQULWEWF
    WGWJXAVDLZWGWEWFYCWFWGWEXNYCXOABCVAVBVEVFWTVGJKLZXAXBKLZVHWTXHVIXAKLZYDYEVJ
    ZYAWTXAFEZYFWSXIXRYHWGXPXSWOWPURRXAVKSXHYBYFYGUTXAJVLVMVNVPVOWSWOTEZWPTEZWQ
    XBVQZWGWSWOXQVRWGWPXTVRJTEYIYJYKVSJWOWPVTWARWBWCWD $.
    $( [24-Dec-2005] $)

  ${
    $d M m $.  $d N k m $.
    $( Geometric lower bound for the factorial function, where N is usually
       held constant.  (Contributed by Paul Chapman, 28-Dec-2007.) $)
    faclbnd6 $p |- ( ( N e. NN0 /\ M e. NN0 ) ->
             ( ( ! ` N ) x. ( ( N + 1 ) ^ M ) ) <_ ( ! ` ( N + M ) ) ) $=
      ( cn0 wcel cfa cfv c1 caddc co cexp cmul cle wbr cc0 wceq oveq2 cr syl wa
      adantr vm vk cv wi oveq2d fveq2d breq12d imbi2d weq cn faccl nnre leid cc
      3syl nn0cn peano2cn exp0 nncn mulid1 eqtrd addid1 3brtr4d peano2nn0 nn0re
      reexpcl sylan remulcl syl2anc nnnn0 nn0ge0 expge0 syl3anc mulge0 syl22anc
      simpr jca nn0addcl readdcl syl2an jca31 adantl wb 0re leadd2 mp3an1 mpbid
      eqbrtrrd 1re leadd1 mp3an3 ax-1cn addass lemul12a sylc expp1 expcl mulass
      breqtrd eqtr4d facp1 3eqtr3d ex expcom a2d nn0ind impcom ) ACDBCDZBEFZBGH
      IZAJIZKIZBAHIZEFZLMZXHXIXJUAUCZJIZKIZBXPHIZEFZLMZUDXHXIXJNJIZKIZBNHIZEFZL
      MZUDXHXIXJUBUCZJIZKIZBYGHIZEFZLMZUDXHXIXJYGGHIZJIZKIZBYMHIZEFZLMZUDXHXOUD
      UAUBAXPNOZYAYFXHYSXRYCXTYELYSXQYBXIKXPNXJJPUEYSXSYDEXPNBHPUFUGUHUAUBUIZYA
      YLXHYTXRYIXTYKLYTXQYHXIKXPYGXJJPUEYTXSYJEXPYGBHPUFUGUHXPYMOZYAYRXHUUAXRYO
      XTYQLUUAXQYNXIKXPYMXJJPUEUUAXSYPEXPYMBHPUFUGUHXPAOZYAXOXHUUBXRXLXTXNLUUBX
      QXKXIKXPAXJJPUEUUBXSXMEXPABHPUFUGUHXHXIXIYCYELXHXIUJDZXIQDZXIXILMBUKZXIUL
      ZXIUMUOXHYCXIGKIZXIXHYBGXIKXHXJUNDZYBGOXHBUNDZUUHBUPZBUQRZXJURRUEXHXIUNDZ
      UUGXIOXHUUCUULUUEXIUSRZXIUTRVAXHYDBEXHUUIYDBOZUUJBVBRZUFVCYGCDZXHYLYRXHUU
      PYLYRUDXHUUPSZYLYRUUQYLSZYIXJKIZYKYPKIZYOYQLUURYIQDZNYILMZSZYKQDZSXJQDZNX
      JLMZSYPQDZSZSZYLXJYPLMZSUUSUUTLMUUQUVIYLUUQUVCUVDUVHUUQUVAUVBUUQUUDYHQDZU
      VAXHUUDUUPXHUUCUUDUUEUUFRTZXHUVEUUPUVKXHXJCDZUVEBVDZXJVERZXJYGVFVGZXIYHVH
      VIUUQUUDNXILMZUVKNYHLMZUVBUVLXHUVQUUPXHUUCUVQUUEUUCXICDUVQXIVJXIVKRRTUVPU
      UQUVEUUPUVFUVRXHUVEUUPUVOTZXHUUPVPXHUVFUUPXHUVMUVFUVNXJVKRTZXJYGVLVMXIYHV
      NVOVQUUQYKUJDZUVDUUQYJCDZUWABYGVRZYJUKRYKULRUUQUVEUVFUVGUVSUVTXHBQDZYMQDZ
      UVGUUPBVEZUUPYMCDUWEYGVDYMVERBYMVSVTWAWATUURYLUVJUUQYLVPUUQUVJYLUUQXJYJGH
      IZYPLUUQBYJLMZXJUWGLMZUUQYDBYJLXHUUNUUPUUOTUUQNYGLMZYDYJLMZUUPUWJXHYGVKWB
      UUQYGQDZUWDUWJUWKWCZUUPUWLXHYGVEWBXHUWDUUPUWFTZNQDUWLUWDUWMWDNYGBWEWFVIWG
      WHUUQUWDYJQDZUWHUWIWCZUWNUUQUWBUWOUWCYJVERUWDUWOGQDUWPWIBYJGWJWKVIWGXHUUI
      YGUNDZUWGYPOZUUPUUJYGUPUUIUWQGUNDUWRWLBYGGWMWKVTZWSTVQYIYKXJYPWNWOUUQYOUU
      SOYLUUQYOXIYHXJKIZKIZUUSUUQYNUWTXIKXHUUHUUPYNUWTOUUKXJYGWPVGUEUUQUULYHUND
      ZUUHUUSUXAOXHUULUUPUUMTXHUUHUUPUXBUUKXJYGWQVGXHUUHUUPUUKTXIYHXJWRVMWTTUUQ
      YQUUTOYLUUQUWGEFZYKUWGKIZYQUUTUUQUWBUXCUXDOUWCYJXARUUQUWGYPEUWSUFUUQUWGYP
      YKKUWSUEXBTVCXCXDXEXFXG $.
      $( [28-Dec-2007] $)
  $}

  $( The product of two factorials is greater than or equal to the factorial of
     (the floor of) their average. $)
  facavg $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
       ( ! ` ( |_ ` ( ( M + N ) / 2 ) ) ) <_ ( ( ! ` M ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa co cle wbr cfv cfa cr syl 3syl adantr letr syl3anc cc0 adantl
    wi c1 caddc c2 cdiv wo cfl cmul nn0re avgle syl2an nn0addcl rehalfcl reflcl
    flle mpand nn0ge0 halfnneg2 mpbid flge0nn0 syl2anc simpl facwordi 3exp sylc
    wb wceq cn cc faccl nncn mulid1 nnre nnnn0 jca 1re lemul2a mp3anl1 syl21anc
    nnge1 eqbrtrrd remulcl mpan2d 3syld simpr mulid2 lemul1a jaod mpd ) ACDZBCD
    ZEZABUAFZUBUCFZAGHZWLBGHZUDZWLUEIZJIZAJIZBJIZUFFZGHZWHAKDZBKDZWOWIAUGZBUGZA
    BUHUIWJWMXAWNWJWMWPAGHZWQWRGHZXAWJWPWLGHZWMXFWJWKKDZWLKDZXHWJWKCDZXIABUJZWK
    UGZLZWKUKZWLUMMZWJWPKDZXJXBXHWMEXFSWJXIXJXQXNXOWLULMZWJXKXIXJXLXMXOMZWHXBWI
    XDNWPWLAOPUNWJWPCDZWHXFXGSWJXJQWLGHZXTXSWJQWKGHZYAWJXKYBXLWKUOLWJXKXIYBYAVD
    XLXMWKUPMUQWLURUSZWHWIUTXTWHXFXGWPAVAVBVCWJXGWRWTGHZXAWJWRTUFFZWRWTGWHYEWRV
    EZWIWHWRVFDZWRVGDYFAVHZWRVIWRVJMNWJWSKDZWRKDZQWRGHZEZTWSGHZYEWTGHZWIYIWHWIW
    SVFDZYIBVHZWSVKLZRZWHYLWIWHYJYKWHYGYJYHWRVKLZWHYGWRCDYKYHWRVLWRUOMVMNWIYMWH
    WIYOYMYPWSVRLRTKDZYIYLYMYNVNTWSWRVOVPVQVSWJWQKDZYJWTKDZXGYDEXASWJXTWQVFDUUA
    YCWPVHWQVKMZWHYJWIYSNZWHYJYIUUBWIYSYQWRWSVTUIZWQWRWTOPWAWBWJWNWPBGHZWQWSGHZ
    XAWJXHWNUUFXPWJXQXJXCXHWNEUUFSXRXSWIXCWHXERWPWLBOPUNWJXTWIUUFUUGSYCWHWIWCXT
    WIUUFUUGWPBVAVBVCWJUUGWSWTGHZXAWJTWSUFFZWSWTGWIUUIWSVEZWHWIYOWSVGDUUJYPWSVI
    WSWDMRWJYJYIQWSGHZEZTWRGHZUUIWTGHZUUDWIUULWHWIYIUUKYQWIYOWSCDUUKYPWSVLWSUOM
    VMRWHUUMWIWHYGUUMYHWRVRLNYTYJUULUUMUUNVNTWRWSWEVPVQVSWJUUAYIUUBUUGUUHEXASUU
    CYRUUEWQWSWTOPWAWBWFWG $.
    $( [9-Dec-2005] $)

