$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      The greatest common divisor operator
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c gcd $.

  $( Extend the definition of a class to include the greatest common divisor
     operator. $)
  cgcd $a class gcd $.

  ${
    $d n x y z $.
    $( Define the ` gcd ` operator. $)
    df-gcd $a |- gcd = ( x e. ZZ , y e. ZZ |-> if ( ( x = 0 /\ y = 0 ) , 0 ,
             sup ( { n e. ZZ | ( n || x /\ n || y ) } , RR , < ) ) ) $.
  $}

  ${
    $d M n x y $.  $d N n y $.
    $( The value of the ` gcd ` operator. ` ( M gcd N ) ` is the greatest
       common divisor of ` M ` and ` N ` .  If ` M ` and ` N ` are both ` 0 ` ,
       the result is defined conventionally as ` 0 ` .  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    gcdval $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd N ) =
              if ( ( M = 0 /\ N = 0 ) , 0 ,
                   sup ( { n e. ZZ | ( n || M /\ n || N ) } , RR , < ) ) ) $=
      ( vx vy cz cv cc0 wceq wa cdivides wbr crab cr clt cif eqeq1 anbi1d eqidd
      csup cgcd breq2 rabbidv supeq1d ifbieq12d anbi2d df-gcd cc 0cn elexi ltso
      supex ifex ovmpt2 ) DEBCFFDGZHIZEGZHIZJZHAGZUOKLZUTUQKLZJZAFMZNOTZPBHIZCH
      IZJZHUTBKLZUTCKLZJZAFMZNOTZPUAVFURJZHVIVBJZAFMZNOTZPUOBIZUSVNHVEHVQVRUPVF
      URUOBHQRVRHSVRNVDVPOVRVCVOAFVRVAVIVBUOBUTKUBRUCUDUEUQCIZVNVHHVQHVMVSURVGV
      FUQCHQUFVSHSVSNVPVLOVSVOVKAFVSVBVJVIUQCUTKUBUFUCUDUEDEAUGVHHVMHUHUIUJNVLO
      UKULUMUN $.
      $( [10-Nov-2013] $) $( [21-Mar-2011] $)
  $}

  $( The value, by convention, of the ` gcd ` operator when both operands are
     0.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  gcd0val $p |- ( 0 gcd 0 ) = 0 $=
    ( vn cc0 cgcd co wceq wa cv cdivides wbr cz crab cr clt csup wcel 0z gcdval
    cif mp2an eqid iftrue eqtri ) BBCDZBBEZUDFZBAGBHIZUFFAJKLMNZRZBBJOZUIUCUHEP
    PABBQSUDUDUHBEBTZUJUEBUGUASUB $.
    $( [21-Mar-2011] $)

  ${
    $d M n $.  $d N n $.
    $( The value of the ` gcd ` operator when at least one operand is nonzero.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    gcdn0val $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ -. ( M = 0 /\ N = 0 ) ) ->
        ( M gcd N ) = sup ( { n e. ZZ | ( n || M /\ n || N ) } , RR , < ) ) $=
      ( cz wcel wa cc0 wceq wn cgcd co cv cdivides wbr crab clt csup cif gcdval
      cr iffalse sylan9eq ) BDECDEFBGHCGHFZIBCJKUCGALZBMNUDCMNFADOTPQZRUEABCSUC
      GUEUAUB $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d A n w x y z $.  $d S w x $.
    gcdcllem1.1 $e |- S = { z e. ZZ | A. n e. A z || n } $.
    $( Lemma for ~ gcdn0cl , ~ gcddvds and ~ dvdslegcd . $)
    gcdcllem1 $p |- ( ( A C_ ZZ /\ E. n e. A n =/= 0 ) ->
                      ( S =/= (/) /\ E. x e. RR A. y e. S y <_ x ) ) $=
      ( vw cz cv wa cle wbr wral cr c1 wcel cdivides wceq wi wss cc0 wrex c0 1z
      ssel 1dvds syl6 ralrimiv breq1 ralbidv elrab2 biimpri sylancr ne0i adantr
      wne syl neeq1 cbvrexv cabs simprbi adantl simplbi dvdsleabs 3expia sylan2
      cfv imp anassrs com23 ralrimiva ancoms r19.26 pm3.35 a1i rgen ralim ax-mp
      sylbir syl2anc fveq2 breq2d imbi12d cbvralv ralbii ralcom 3bitri sylib cc
      r19.21v zssre sstr mpan2 ssel2 sylan recn abscl wb rcla4edv syldan imim2d
      3syl breq2 mpd r19.23v sylan2b jca ) DIUAZFJZUBUQZFDUCZKEUDUQZBJZAJZLMZBE
      NZAOUCZXIXMXLXIPEQZXMXIPIQZPXJRMZFDNZXSUEXIYAFDXIXJDQZXJIQZYADIXJUFZXJUGU
      HUIXSXTYBKCJZXJRMZFDNZYBCPIEYFPSYGYAFDYFPXJRUJUKGULUMUNEPUOURUPXLXIHJZUBU
      QZHDUCZXRXKYJFHDXJYIUBUSZUTXIYKXRXIYJXRTZHDNZYKXRTXIYJXNYIVAVHZLMZBENZTZH
      DNZYNXIXKXNXJVAVHZLMZTZFDNZBENZYSXIUUCBEXIXNEQZKXNXJRMZFDNZUUFUUBTZFDNZUU
      CUUEUUGXIUUEXNIQZUUGYHUUGCXNIEYFXNSYGUUFFDYFXNXJRUJUKGULZVBVCUUEXIUUJUUIU
      UEUUJUUGUUKVDUUJXIUUIUUJXIKZUUHFDUULYCKXKUUFUUAUUJXIYCXKUUFUUATZTZXIYCKUU
      JYDUUNXIYCYDYEVIUUJYDXKUUMXNXJVEVFVGVJVKVLVMVGUUGUUIKUUFUUHKZFDNZUUCUUFUU
      HFDVNUUOUUBTZFDNUUPUUCTUUQFDUUQYCUUFUUBVOVPVQUUOUUBFDVRVSVTWAVLUUDYJYPTZH
      DNZBENUURBENZHDNYSUUCUUSBEUUBUURFHDXJYISZXKYJUUAYPYLUVAYTYOXNLXJYIVAWBWCW
      DWEWFUURBHEDWGUUTYRHDYJYPBEWKWFWHWIXIYRYMTZHDNYSYNTXIUVBHDXIYIDQZKZYQXRYJ
      XIUVCYOOQZYQXRTUVDYIOQZYIWJQUVEXIDOUAZUVCUVFXIIOUAUVGWLDIOWMWNDOYIWOWPYIW
      QYIWRXCXIXQYQAYOOXOYOSZXQYQWSXIUVHXPYPBEXOYOXNLXDUKVCWTXAXBVLYRYMHDVRURXE
      YJXRHDXFWIVIXGXH $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K z $.  $d M n x y z $.  $d N n x y z $.  $d R x y $.  $d S x y $.
    gcdcllem2.1 $e |- S = { z e. ZZ | A. n e. { M , N } z || n } $.
    gcdcllem2.2 $e |- R = { z e. ZZ | ( z || M /\ z || N ) } $.
    $( Lemma for ~ gcdn0cl , ~ gcddvds and ~ dvdslegcd . $)
    gcdcllem2 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> R = S ) $=
      ( vx cz wcel wa cv cdivides wbr wral wceq breq1 elrab2 wi prid1g anim12ii
      cpr ralbidv prid2g breq2 rcla4v syl2an wo vex elpr biimprcd jaao ralrimiv
      syl5bi a1i impbid anbi2d syl5bb anbi12d syl6rbbr eqrdv ) EJKZFJKZLZIBCVEI
      MZCKZVFJKZVFENOZVFFNOZLZLZVFBKVGVHVFDMZNOZDEFUCZPZLVEVLAMZVMNOZDVOPVPAVFJ
      CVQVFQZVRVNDVOVQVFVMNRUDGSVEVPVKVHVEVPVKVCEVOKZFVOKZVPVKTVDEFJUAEFJUEVTVP
      VIWAVJVNVIDEVOVMEVFNUFZUGVNVJDFVOVMFVFNUFZUGUBUHVKVPTVEVKVNDVOVMVOKVMEQZV
      MFQZUIVKVNVMEFDUJUKVIWDVNVJWEWDVNVIWBULWEVNVJWCULUMUOUNUPUQURUSVQENOZVQFN
      OZLVKAVFJBVSWFVIWGVJVQVFENRVQVFFNRUTHSVAVB $.
      $( [21-Mar-2011] $)

    $( Lemma for ~ gcdn0cl , ~ gcddvds and ~ dvdslegcd . $)
    gcdcllem3 $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ -. ( M = 0 /\ N = 0 ) ) ->
        ( sup ( R , RR , < ) e. NN /\
          ( sup ( R , RR , < ) || M /\ sup ( R , RR , < ) || N ) /\
          ( ( K e. ZZ /\ K || M /\ K || N ) -> K <_ sup ( R , RR , < ) ) ) ) $=
      ( vy vx cz wcel wa cc0 wceq cdivides wbr c1 breq1 wn cr clt cn w3a cle wi
      csup c0 wne cv wral wrex cpr wss wal vex elpr eleq1 biimprcd jaao alrimiv
      wo syl5bi dfss2 sylibr adantr neorian prid1g neeq1 rcla4ev adantlr prid2g
      sylan adantll jaodan sylan2br gcdcllem1 syl2anc gcdcllem2 rexbidv anbi12d
      wb raleq syl mpbird crab ssrab2 eqsstri suprzcl mp3an1 eqtri elrab2 sylib
      cbvrabv simpld 1dvds anim12i 1z zssre sstri suprub ex mpd elnnz1 sylanbrc
      mpbiran simprd biimpri 3impb syl2im 3jca ) FLMZGLMZNZFOPGOPNUAZNZBUBUCUHZ
      UDMZXRFQRZXRGQRZNZELMZEFQRZEGQRZUEZEXRUFRZUGXQXRLMZSXRUFRZXSXQYHYBXQXRBMZ
      YHYBNXQBUIUJZJUKKUKZUFRZJBULZKUBUMZNZYJXQYPCUIUJZYMJCULZKUBUMZNZXQFGUNZLU
      OZDUKZOUJZDUUAUMZYTXOUUBXPXOYLUUAMZYLLMZUGZKUPUUBXOUUHKUUFYLFPZYLGPZVCXOU
      UGYLFGKUQURXMUUIUUGXNUUJUUIUUGXMYLFLUSUTUUJUUGXNYLGLUSUTVAVDVBKUUALVEVFVG
      XPXOFOUJZGOUJZVCUUEFOGOVHXOUUKUUEUULXMUUKUUEXNXMFUUAMUUKUUEFGLVIUUDUUKDFU
      UAUUCFOVJVKVNVLXNUULUUEXMXNGUUAMUULUUEFGLVMUUDUULDGUUAUUCGOVJVKVNVOVPVQKJ
      AUUACDHVRVSXOYPYTWCZXPXOBCPZUUMABCDFGHIVTUUNYKYQYOYSBCUIVJUUNYNYRKUBYMJBC
      WDWAWBWEVGWFZBLUOYKYOYJBAUKZFQRZUUPGQRZNZALWGZLIUUSALWHWIZKJBWJWKWEYLFQRZ
      YLGQRZNZYBKXRLBYLXRPUVBXTUVCYAYLXRFQTYLXRGQTWBBUUTUVDKLWGIUUSUVDAKLUUPYLP
      UUQUVBUURUVCUUPYLFQTUUPYLGQTWBWOWLWMWNZWPXQSBMZYIXOUVFXPXOSFQRZSGQRZNZUVF
      XMUVGXNUVHFWQGWQWRUVFSLMUVIUUSUVIASLBUUPSPUUQUVGUURUVHUUPSFQTUUPSGQTWBIWM
      WSXGVFVGXQYPUVFYIUGZUUOBUBUOZYKYOUVJBLUBUVAWTXAZUVKYKYOUEZUVFYIKJBSXBXCWK
      WEXDXRXEXFXQYHYBUVEXHXQYPYFEBMZYGUUOYCYDYEUVNUVNYCYDYENZNUUSUVOAELBUUPEPU
      UQYDUURYEUUPEFQTUUPEGQTWBIWMXIXJUVKYKYOUVNYGUGUVLUVMUVNYGKJBEXBXCWKXKXL
      $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K n $.  $d M n z $.  $d N n z $.
    $( Closure of the ` gcd ` operator.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    gcdn0cl $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ -. ( M = 0 /\ N = 0 ) ) ->
                    ( M gcd N ) e. NN ) $=
      ( vn cK vz cz wcel wa cc0 wceq wn cgcd co cv cdivides wbr crab cr cn eqid
      clt csup gcdn0val w3a cle wi cpr wral gcdcllem3 simp1d eqeltrd ) AFGBFGHA
      IJBIJHKHZABLMCNZAOPUMBOPHCFQZRUAUBZSCABUCULUOSGUOAOPUOBOPHDFGDAOPDBOPUDDU
      OUEPUFCUNUMENOPEABUGUHCFQZEDABUPTUNTUIUJUK $.
      $( [21-Mar-2011] $)

    $( The gcd of two integers divides each of them.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    gcddvds $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                    ( ( M gcd N ) || M /\ ( M gcd N ) || N ) ) $=
      ( vn cK vz cz wcel wa cc0 wceq cgcd co cdivides wbr breq1d anbi12d mpbird
      breq2 cv crab 0z dvds0 ax-mp bi2anan9 anidm syl6bb mpbiri oveq2 sylan9eqr
      oveq1 gcd0val syl6eq adantl wn cr clt csup cn w3a cle wral eqid gcdcllem3
      wi cpr simp2d gcdn0val pm2.61dan ) AFGBFGHZAIJZBIJZHZABKLZAMNZVMBMNZHZVLV
      PVIVLVPIAMNZIBMNZHZVLVSIIMNZIFGVTUAIUBUCVLVSVTVTHVTVJVQVTVKVRVTAIIMRBIIMR
      UDVTUEUFUGVLVNVQVOVRVLVMIAMVLVMIIKLZIVKVJVMAIKLWABIAKUHAIIKUJUIUKULZOVLVM
      IBMWBOPQUMVIVLUNHZVPCSZAMNWDBMNHCFTZUOUPUQZAMNZWFBMNZHZWCWFURGWIDFGDAMNDB
      MNUSDWFUTNVDCWEWDESMNEABVEVACFTZEDABWJVBWEVBVCVFWCVNWGVOWHWCVMWFAMCABVGZO
      WCVMWFBMWKOPQVH $.
      $( [21-Mar-2011] $)

    $( An integer which divides both operands of the ` gcd ` operator is
       bounded by it.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdslegcd $p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\
                        -. ( M = 0 /\ N = 0 ) ) ->
                      ( ( K || M /\ K || N ) -> K <_ ( M gcd N ) ) ) $=
      ( vn vz cz wcel w3a cc0 wceq wa wn cdivides wbr cle wi cv crab eqid com12
      cgcd co cr clt csup cn cpr gcdcllem3 simp3d gcdn0val breq2d sylibrd 3expb
      wral exp3a ex com23 impcom 3impb imp ) AFGZBFGZCFGZHBIJCIJKLZABMNZACMNZKZ
      ABCUAUBZONZPZVAVBVCVDVJPZVBVCKZVAVKVLVDVAVJVLVDVAVJPVLVDKZVAVGVIVAVGKVMVI
      VAVEVFVMVIPVMVAVEVFHZVIVMVNADQZBMNVOCMNKDFRZUCUDUEZONZVIVMVQUFGVQBMNVQCMN
      KVNVRPDVPVOEQMNEBCUGUNDFRZEABCVSSVPSUHUIVMVHVQAODBCUJUKULTUMTUOUPUQURUSUT
      $.
      $( [21-Mar-2011] $)
  $}

  $( Closure of the ` gcd ` operator.  (Contributed by Paul Chapman,
     21-Mar-2011.) $)
  gcdcl $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd N ) e. NN0 ) $=
    ( cz wcel wa cc0 wceq cgcd co cn0 0nn0 oveq2 oveq1 sylan9eqr gcd0val syl6eq
    eleq1d mpbiri adantl wn cn gcdn0cl nnnn0 syl pm2.61dan ) ACDBCDEZAFGZBFGZEZ
    ABHIZJDZUIUKUFUIUKFJDKUIUJFJUIUJFFHIZFUHUGUJAFHIULBFAHLAFFHMNOPQRSUFUITEUJU
    ADUKABUBUJUCUDUE $.
    $( [21-Mar-2011] $)

  ${
    $d n x y $.
    $( Domain and codomain of the ` gcd ` operator.  (Contributed by Paul
       Chapman, 31-Mar-2011.) $)
    gcdf $p |- gcd : ( ZZ X. ZZ ) --> NN0 $=
      ( vx vy vn cv cc0 wceq wa cdivides wbr cz crab clt csup cif cn0 wcel wral
      cr cxp cgcd wf co gcdval gcdcl eqeltrrd rgen2a df-gcd fmpt2 mpbi ) ADZEFB
      DZEFGECDZUJHIULUKHIGCJKRLMNZOPZBJQAJQJJSOTUAUNABJUJJPUKJPGUJUKTUBUMOCUJUK
      UCUJUKUDUEUFABJJUMOTABCUGUHUI $.
      $( [16-Nov-2013] $) $( [31-Mar-2011] $)
  $}

  ${
    $d M n $.  $d N n $.
    $( The ` gcd ` operator is commutative.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    gcdcom $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd N ) = ( N gcd M ) ) $=
      ( vn cz wcel wa cc0 wceq cdivides wbr crab cr clt csup cif cgcd ancom a1i
      co gcdval cv wb rabbiia supeq1i ifbieq2i ancoms 3eqtr4d ) ADEZBDEZFZAGHZB
      GHZFZGCUAZAIJZUNBIJZFZCDKZLMNZOZULUKFZGUPUOFZCDKZLMNZOZABPSBAPSZUTVEHUJUM
      VAUSVDGUKULQLURVCMUQVBCDUQVBUBUNDEUOUPQRUCUDUERCABTUIUHVFVEHCBATUFUG $.
      $( [22-Jun-2011] $) $( [21-Mar-2011] $)
  $}

  $( The gcd of two integers is zero iff they are both zero.  (Contributed by
     Paul Chapman, 21-Mar-2011.) $)
  gcdeq0 $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                 ( ( M gcd N ) = 0 <-> ( M = 0 /\ N = 0 ) ) ) $=
    ( cz wcel wa cgcd co cc0 wceq wne wn nne cn gcdn0cl nnne0 syl con1d syl5bir
    ex oveq12 gcd0val syl6eq impbid1 ) ACDBCDEZABFGZHIZAHIBHIEZUFUEHJZKUDUGUEHL
    UDUGUHUDUGKZUHUDUIEUEMDUHABNUEOPSQRUGUEHHFGHAHBHFTUAUBUC $.
    $( [21-Mar-2011] $)

  $( The gcd of two integers is positive (nonzero) iff they are not both zero.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  gcdn0gt0 $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                   ( -. ( M = 0 /\ N = 0 ) <-> 0 < ( M gcd N ) ) ) $=
    ( cz wcel wa cc0 cgcd co clt wbr wne wceq wn cn0 wb gcdcl cle nn0re nn0ge0
    cr 0re leltne mp3an1 syl2anc syl df-ne gcdeq0 notbid syl5bb bitr2d ) ACDBCD
    EZFABGHZIJZULFKZAFLBFLEZMZUKULNDZUMUNOZABPUQULTDZFULQJZURULRULSFTDUSUTURUAF
    ULUBUCUDUEUNULFLZMUKUPULFUFUKVAUOABUGUHUIUJ $.
    $( [22-Jun-2011] $)

  $( The gcd of 0 and an integer is the integer's absolute value.  (Contributed
     by Paul Chapman, 21-Mar-2011.) $)
  gcd0id $p |- ( N e. ZZ -> ( 0 gcd N ) = ( abs ` N ) ) $=
    ( cz wcel cc0 wceq cgcd co cabs cfv syl6eq wn wa cle wbr wb 0z syl cdivides
    adantr wi oveq2 gcd0val fveq2 abs0 eqtr4d adantl wne df-ne cr cn0 mpan nn0z
    gcdcl zre nn0abscl letri3 syl2anc gcddvds simprd dvdsleabs syl3an1 3anidm12
    mpd dvds0 iddvds absdvdsb anidms mpbid eqid biantrur notbii bitri dvdslegcd
    jca w3a ex mp3an2 mpancom syl5bi imp mpbir2and sylan2br pm2.61dan ) ABCZADE
    ZDAFGZAHIZEZWEWHWDWEWFDWGWEWFDDFGDADDFUAUBJWEWGDHIDADHUCUDJUEUFWEKZWDADUGZW
    HADUHZWDWJLZWHWFWGMNZWGWFMNZWDWHWMWNLOZWJWDWFUICZWGUICZWOWDWFBCZWPWDWFUJCZW
    RDBCZWDWSPDAUMUKWFULQZWFUNQWDWGBCZWQWDWGUJCXBAUOWGULQZWGUNQWFWGUPUQSWLWFARN
    ZWMWDXDWJWDWFDRNZXDWTWDXEXDLPDAURUKUSSWDWJXDWMTZWDWRWDWJXFXAWFAUTVAVBVCWLWG
    DRNZWGARNZLZWNWDXIWJWDXGXHWDXBXGXCWGVDQWDAARNZXHAVEWDXJXHOAAVFVGVHVNSWDWJXI
    WNTZWJDDEZWELZKZWDXKWJWIXNWKWEXMXLWEDVIVJVKVLXBWDXNXKTZXCXBWTWDXOPXBWTWDVOX
    NXKWGDAVMVPVQVRVSVTVCWAWBWC $.
    $( [21-Mar-2011] $)

  $( The gcd of an integer and 0 is the integer's absolute value.  (Contributed
     by Paul Chapman, 31-Mar-2011.) $)
  gcdid0 $p |- ( N e. ZZ -> ( N gcd 0 ) = ( abs ` N ) ) $=
    ( cz wcel cc0 cgcd co cabs cfv wceq 0z gcdcom mpan gcd0id eqtr3d ) ABCZDAEF
    ZADEFZAGHDBCOPQIJDAKLAMN $.
    $( [31-Mar-2011] $)

  $( The gcd of a nonnegative integer with 0 is itself.  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  nn0gcdid0 $p |- ( N e. NN0 -> ( N gcd 0 ) = N ) $=
    ( cn0 wcel cc0 cgcd co cabs cfv cz wceq nn0z 0z gcdcom gcd0id eqtr3d syl cr
    mpan cle wbr nn0re nn0ge0 absid syl2anc eqtrd ) ABCZADEFZAGHZAUFAICZUGUHJAK
    UIDAEFZUGUHDICUIUJUGJLDAMRANOPUFAQCDASTUHAJAUAAUBAUCUDUE $.
    $( [31-Mar-2011] $)

  $( Negating one operand of the ` gcd ` operator does not alter the result.
     (Contributed by Paul Chapman, 21-Mar-2011.) $)
  gcdneg $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd -u N ) = ( M gcd N ) ) $=
    ( cz wcel wa cgcd co cc0 wceq oveq12 adantl syl anbi2d cle syl2anc cdivides
    wb wn wbr wi cneg cc zcn negeq0 syl6bi imp eqtr4d cn0 gcdcl nn0z zre znegcl
    cr sylan2 letri3 adantr gcddvds simpr dvdsnegb mpbid notbid simpl dvdslegcd
    w3a ex syl3anc sylbid com12 mpdi impcom mpbird mpbir2and pm2.61dan eqcomd )
    ACDZBCDZEZABFGZABUAZFGZVQAHIZBHIZEZVRVTIZVQWCEVRHHFGZVTWCVRWEIVQAHBHFJKVQWC
    VTWEIZVQWCWAVSHIZEZWFVPWCWHQVOVPWBWGWAVPBUBDWBWGQBUCBUDLMKZAHVSHFJUEUFUGVQW
    CRZEWDVRVTNSZVTVRNSZVQWDWKWLEQZWJVQVRUMDZVTUMDZWMVQVRCDZWNVQVRUHDWPABUIVRUJ
    LZVRUKLVQVTCDZWOVPVOVSCDZWRBULZVOWSEVTUHDWRAVSUIVTUJLUNZVTUKLVRVTUOOUPWJVQW
    KWJVQVRAPSZVRVSPSZEZWKVQXBVRBPSZEXDABUQVQXEXCXBVQWPVPXEXCQWQVOVPURZVRBUSOMU
    TVQWJXDWKTZVQWJWHRZXGVQWCWHWIVAVQWPVOWSXHXGTWQVOVPVBZVPWSVOWTKWPVOWSVDXHXGV
    RAVSVCVEVFVGVHVIVJWJVQWLWJVQVTAPSZVTBPSZEZWLVQXLXJVTVSPSZEZVPVOWSXNWTAVSUQU
    NVQXKXMXJVQWRVPXKXMQXAXFVTBUSOMVKVQWJXLWLTZVQWRVOVPWJXOTXAXIXFWRVOVPVDWJXOV
    TABVCVEVFVHVIVJVLVMVN $.
    $( [21-Mar-2011] $)

  $( Negating one operand of the ` gcd ` operator does not alter the result.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  neggcd $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( -u M gcd N ) = ( M gcd N ) ) $=
    ( cz wcel wa cneg cgcd co wceq gcdneg ancoms znegcl gcdcom sylan 3eqtr4d )
    ACDZBCDZEBAFZGHZBAGHZRBGHZABGHQPSTIBAJKPRCDQUASIALRBMNABMO $.
    $( [22-Jun-2011] $)

  ${
    gcdaddmlem.1 $e |- K e. ZZ $.
    gcdaddmlem.2 $e |- M e. ZZ $.
    gcdaddmlem.3 $e |- N e. ZZ $.
    $( Lemma for ~ gcdaddm . $)
    gcdaddmlem $p |- ( M gcd N ) = ( M gcd ( ( K x. M ) + N ) ) $=
      ( cc0 wceq cmul co caddc wa cgcd wbr cdivides cz wcel mp2an wi ax-mp nn0z
      wn cle gcddvds simpli c1 cn0 gcdcl w3a 1z dvds2ln mpanl12 mp3an cc oveq2i
      zcn mulid2i breqtri pm3.2i zmulcl zaddcl dvdslegcd ex mpi znegcl mulneg1i
      oveq12i mulcli negcli addcomi negidi eqtri oveq1i addassi addid2i 3eqtr3i
      anim12i cr zre letri3i sylibr wo pm4.57 oveq2 mul01i syl6eq oveq1d eqeq1d
      cneg pm5.32i oveq12 sylbir eqtr4d sylbi jaoi pm2.61i ) BGHZABIJZCKJZGHZLZ
      UBZWQCGHZLZUBZLZBCMJZBWSMJZHZXFXGXHUCNZXHXGUCNZLXIXBXJXEXKXBXGBONZXGWSONZ
      LZXJXLXMXLXGCONZBPQZCPQZXLXOLZEFBCUDRZUEXGWRUFCIJZKJZWSOXRXGYAONZXSXGPQZX
      PXQXRYBSZXGUGQZYCXPXQYEEFBCUHRXGUATZEFAPQZUFPQZYCXPXQUIYDDUJAUFXGBCUKULUM
      TXTCWRKCXQCUNQFCUPTZUQUOURUSYCXPWSPQZXBXNXJSZSYFEWRPQZXQYJYGXPYLDEABUTRFW
      RCVARZYCXPYJUIXBYKXGBWSVBVCUMVDXEXHBONZXHCONZLZXKYNYOYNXHWSONZXPYJYNYQLZE
      YMBWSUDRZUEXHAWIZBIJZUFWSIJZKJZCOYRXHUUCONZYSXHPQZXPYJYRUUDSZXHUGQZUUEXPY
      JUUGEYMBWSUHRXHUATZEYMYTPQZYHUUEXPYJUIUUFYGUUIDAVETUJYTUFXHBWSUKULUMTUUCW
      RWIZWSKJZCUUAUUJUUBWSKABYGAUNQDAUPTZXPBUNQEBUPTZVFWSYJWSUNQYMWSUPTUQVGUUJ
      WRKJZCKJGCKJZUUKCUUNGCKUUNWRUUJKJGUUJWRWRABUULUUMVHZVIZUUPVJWRUUPVKVLVMUU
      JWRCUUQUUPYIVNCYIVOZVPVLURUSUUEXPXQXEYPXKSZSUUHEFUUEXPXQUIXEUUSXHBCVBVCUM
      VDVQXGXHYCXGVRQYFXGVSTUUEXHVRQUUHXHVSTVTWAXFUBXAXDWBXIXAXDWCXAXIXDXAXDXIW
      QWTXCWQWSCGWQWSUUOCWQWRGCKWQWRAGIJGBGAIWDAUULWEWFWGUURWFWHWJZXDXGGGMJZXHB
      GCGMWKXDXAXHUVAHUUTBGWSGMWKWLWMZWNUVBWOWNWP $.
      $( [31-Mar-2011] $)
  $}

  $( Adding a multiple of one operand of the ` gcd ` operator to the other does
     not alter the result.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
  gcdaddm $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                  ( M gcd N ) = ( M gcd ( N + ( K x. M ) ) ) ) $=
    ( cz wcel cgcd co cmul caddc cc0 cif oveq1 oveq1d oveq2d oveq2 0z elimel cc
    wceq zcn eqeq2d id oveq12d eqeq12d gcdaddmlem dedth3h wa mulcl syl2an 3impa
    w3a addcom eqtrd ) ADEZBDEZCDEZUKZBCFGZBABHGZCIGZFGZBCUSIGZFGUNUOUPURVASURB
    UNAJKZBHGZCIGZFGZSUOBJKZCFGZVGVCVGHGZCIGZFGZSVGUPCJKZFGZVGVIVLIGZFGZSABCJJJ
    AVCSZVAVFURVPUTVEBFVPUSVDCIAVCBHLMNUABVGSZURVHVFVKBVGCFLVQBVGVEVJFVQUBVQVDV
    ICIBVGVCHOMUCUDCVLSZVHVMVKVOCVLVGFOVRVJVNVGFCVLVIIONUDVCVGVLAJDPQBJDPQCJDPQ
    UEUFUQUTVBBFUNUOUPUTVBSZUNUOUGUSREZCREVSUPUNAREBREVTUOATBTABUHUICTUSCULUIUJ
    NUM $.
    $( [31-Mar-2011] $)

  $( The gcd of a number and itself is its absolute value.  (Contributed by
     Paul Chapman, 31-Mar-2011.) $)
  gcdid $p |- ( N e. ZZ -> ( N gcd N ) = ( abs ` N ) ) $=
    ( cz wcel cc0 cgcd co c1 cmul caddc cabs cfv wceq 1z gcdaddm mp3an13 gcdid0
    0z cc zcn oveq2d mulid2 addid2 eqtrd syl 3eqtr3rd ) ABCZADEFZADGAHFZIFZEFZA
    JKAAEFGBCUFDBCUGUJLMQGADNOAPUFUIAAEUFARCZUIALASUKUIDAIFAUKUHADIAUATAUBUCUDT
    UE $.
    $( [31-Mar-2011] $)
  $( The gcd of a number with 1 is 1.  (Contributed by Mario Carneiro,
     19-Feb-2014.) $)
  gcd1 $p |- ( M e. ZZ -> ( M gcd 1 ) = 1 ) $=
    ( cz wcel c1 cgcd co cle wbr wceq cdivides wa 1z gcddvds mpan2 simprd cn wi
    cc0 wn syl wne ax-1ne0 simpr necon3ai gcdn0cl nnz 1nn dvdsle sylancl mpd wb
    ax-mp nnle1eq1 mpbid ) ABCZADEFZDGHZUPDIZUOUPDJHZUQUOUPAJHZUSUODBCZUTUSKLAD
    MNOUOUPBCZDPCUSUQQUOUPPCZVBUOVAVCLUOVAKARIZDRIZKZSZVCDRUAVGUBVFDRVDVEUCUDUL
    ADUENNZUPUFTUGUPDUHUIUJUOVCUQURUKVHUPUMTUN $.
    $( [19-Feb-2014] $)

  $( The gcd of two integers is the same as that of their absolute values.
     (Contributed by Paul Chapman, 31-Mar-2011.) $)
  gcdabs $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                 ( ( abs ` M ) gcd ( abs ` N ) ) = ( M gcd N ) ) $=
    ( cz wcel wa cabs cfv wceq cneg wo cgcd co cr absor oveq12 gcdneg sylan9eqr
    zre gcdcom ex anim12i syl2an wi a1i ancoms znegcl sylan 3eqtr4d ccased mpd
    eqtrd ) ACDZBCDZEZAFGZAHZUOAIZHZJZBFGZBHZUTBIZHZJZEZUOUTKLZABKLZHZULAMDZBMD
    ZVEUMARBRVIUSVJVDANBNUAUBUNUPVAURVCVHUPVAEVHUCUNUOAUTBKOUDUNURVAEZVHVKUNVFU
    QBKLZVGUOUQUTBKOUNBUQKLZBAKLZVLVGUMULVMVNHBAPUEULUQCDZUMVLVMHAUFZUQBSUGABSU
    HZQTUNUPVCEZVHVRUNVFAVBKLVGUOAUTVBKOABPQTUNURVCEZVHVSUNVFUQVBKLZVGUOUQUTVBK
    OUNVTVLVGULVOUMVTVLHVPUQBPUGVQUKQTUIUJ $.
    $( [31-Mar-2011] $)

  $( The gcd remains unchanged if one operand is replaced with its remainder
     modulo the other.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
  modgcd $p |- ( ( M e. ZZ /\ N e. NN ) ->
                 ( ( M mod N ) gcd N ) = ( M gcd N ) ) $=
    ( cz wcel wa co cgcd cneg cmul caddc cr wceq recnd adantl syl eqtrd syl3anc
    cc oveq2d eqtr4d cn cmo cdiv cfl cfv cmin crp zre nnrp modval syl2an adantr
    nnre cc0 wne nnne0 redivcl syl3an 3anidm23 zcn mulneg1 mulcom negeqd ancoms
    flcl w3a 3adant1 mulcl negsub sylan2 3impb znegcl simpl gcdaddm zmodcl nn0z
    nnz cn0 gcdcom syl2anc 3eqtr3d ) ACDZBUADZEZBABUBFZGFZBAGFZWEBGFZABGFZWDWFB
    AABUCFZUDUEZHZBIFZJFZGFZWGWDWEWNBGWDWEABWKIFZUFFZWNWBAKDZBUGDWEWQLWCAUHZBUI
    ABUJUKWDARDZBRDZWKRDZWNWQLWBWTWCWBAWSMULWCXAWBWCBBUMZMNWDWKCDZXBWDWJKDZXDWB
    WCXEWBWRWCBKDWCBUNUOXEWSXCBUPABUQURUSWJVEOZWKUTOWTXAXBVFZWNAWPHZJFZWQXGWMXH
    AJXAXBWMXHLZWTXBXAXJXBXAEZWMWKBIFZHXHWKBVAXKXLWPWKBVBVCPVDVGSWTXAXBXIWQLZXA
    XBEWTWPRDXMBWKVHAWPVIVJVKPQTSWDWLCDZBCDZWBWGWOLWDXDXNXFWKVLOWCXOWBBVQNZWBWC
    VMZWLBAVNQTWDXOWECDZWFWHLXPWDWEVRDXRABVOWEVPOBWEVSVTWDXOWBWGWILXPXQBAVSVTWA
    $.
    $( [31-Mar-2011] $)
  ${
    $d s t u v x y z A $.  $d s t u v x y z B $.
    ${
      $d s t u v x y z G $.  $d s t u v M $.  $d s t u v x y z ph $.
      $d s t u v x y z C $.
      bezout.1 $e |- M = { z e. NN | E. x e. ZZ E. y e. ZZ
                           z = ( ( A x. x ) + ( B x. y ) ) } $.
      bezout.3 $e |- ( ph -> A e. ZZ ) $.
      bezout.4 $e |- ( ph -> B e. ZZ ) $.
      $( Lemma for ~ bezout . $)
      bezoutlem1 $p |- ( ph -> ( A =/= 0 -> ( abs ` A ) e. M ) ) $=
        ( cmul co wceq cz wrex cc0 wcel c1 cc syl cabs cfv cv caddc fveq2 oveq1
        wne cn eqeq12d rexbidv cr zre cneg wo absor ax-1rid eqcomd oveq2 eqeq2d
        1z rcla4ev sylancr eqeq1 syl5ibrcom znegcl ax-mp recn zcn mulcom eqtr3d
        mulm1 jaod mpd vtoclga wa adantr mul01 oveq2d mulcl syl2an addid1 eqtrd
        0z mpan syl6bir reximdva wi nnabscl 2rexbidv elrab2 simplbi2com sylsyld
        ex ) AEUAUBZEBUCZKLZFCUCZKLZUDLZMZCNOZBNOZEPUGZWNUHQZWNGQZAWNWPMZBNOZXB
        AENQZXGIDUCZUAUBZXIWOKLZMZBNOZXGDENXIEMZXLXFBNXNXJWNXKWPXIEUAUEXIEWOKUF
        UIUJXINQXIUKQZXMXIULXOXJXIMZXJXIUMZMZUNXMXIUOXOXPXMXRXOXMXPXIXKMZBNOZXO
        RNQZXIXIRKLZMZXTUTXOYBXIXIUPUQXSYCBRNWORMXKYBXIWORXIKURUSVAVBXPXLXSBNXJ
        XIXKVCUJVDXOXMXRXQXKMZBNOZXORUMZNQZXQXIYFKLZMZYEYAYGUTRVEVFZXOYFXIKLZXQ
        YHXOXISQZYKXQMXIVGZXIVKTXOYFSQZYLYKYHMYGYNYJYFVHVFYMYFXIVIVBVJYDYIBYFNW
        OYFMXKYHXQWOYFXIKURUSVAVBXRXLYDBNXJXQXKVCUJVDVLVMTVNTAXFXABNAWONQZVOZXF
        WNWPFPKLZUDLZMZXAYPYRWPWNYPYRWPPUDLZWPYPYQPWPUDYPFSQZYQPMAUUAYOAFNQUUAJ
        FVHTVPFVQTVRYPWPSQZYTWPMAESQZWOSQUUBYOAXHUUCIEVHTWOVHEWOVSVTWPWATWBUSPN
        QYSXAWCWTYSCPNWQPMZWSYRWNUUDWRYQWPUDWQPFKURVRUSVAWDWEWFVMAXHXCXDWGIXHXC
        XDEWHWMTXEXDXBXIWSMZCNOBNOXBDWNUHGXIWNMUUEWTBCNNXIWNWSVCWIHWJWKWL $.
        $( [15-Mar-2014] $)

      bezout.2 $e |- G = sup ( M , RR , `' < ) $.
      bezout.5 $e |- ( ph -> -. ( A = 0 /\ B = 0 ) ) $.
      $( Lemma for ~ bezout . $)
      bezoutlem2 $p |- ( ph -> G e. M ) $=
        ( wcel cn wceq cz wrex cc0 cc cr clt ccnv csup c1 cuz cfv wss c0 wne cv
        cmul co caddc crab ssrab2 eqsstri nnuz sseqtri wo wa neorian bezoutlem1
        sylibr cabs ne0i syl6 eqid rexcom zcn syl adantr ad2antll mulcl syl2anc
        ad2antrl addcom eqeq2d 2rexbidva syl5bb rabbidv syl5eq sylibrd jaod mpd
        wn eleq2d infmssuzcl sylancr syl5eqel ) AGHUAUBUCUDZHLAHUEUFUGZUHHUIUJZ
        WKHNHOWLHDUKZEBUKZULUMZFCUKZULUMZUNUMZPZCQRBQRZDOUOZOIXADOUPUQURUSAESUJ
        ZFSUJZUTZWMAESPFSPVAWFXEMESFSVBVDAXCWMXDAXCEVEUGZHNWMABCDEFHIJKVCHXFVFV
        GAXDFVEUGZHNZWMAXDXGWNWRWPUNUMZPZBQRCQRZDOUOZNXHACBDFEXLXLVHKJVCAHXLXGA
        HXBXLIAXAXKDOXAWTBQRCQRAXKWTBCQQVIAWTXJCBQQAWQQNZWOQNZVAZVAZWSXIWNXPWPT
        NZWRTNZWSXIPXPETNZWOTNZXQAXSXOAEQNXSJEVJVKVLXNXTAXMWOVJVMEWOVNVOXPFTNZW
        QTNZXRAYAXOAFQNYAKFVJVKVLXMYBAXNWQVJVPFWQVNVOWPWRVQVOVRVSVTWAWBWGWCHXGV
        FVGWDWEHUEWHWIWJ $.
        $( [15-Mar-2014] $)

      $d M x y $.
      $( Lemma for ~ bezout . $)
      bezoutlem3 $p |- ( ph -> ( C e. M -> G || C ) ) $=
        ( wcel co wceq cmul caddc cz vs vt vu vv cdivides wbr wa cmo cc0 cn cle
        wn clt cr crp cv wrex simpr eqeq1 2rexbidv oveq1d eqeq2d rexbidv oveq2d
        oveq2 syl6bb elrab2 sylib simpld nnre syl bezoutlem2 syl5bb nnrp adantr
        cbvrexv modlt syl2anc wb cn0 nnz zmodcl nn0re ltnle mpbid cdiv cfl cmin
        cfv simprd ad2antrr simprll simprrl nndivre flcl zmulcl simprlr simprrr
        wi zsubcl cc mulcl addsub4 syl22anc adddir syl3anc mulass oveq12d eqtrd
        subdi 3eqtr4d rcla42ev oveq1 oveq12 sylan2 eqeq1d syl5ibrcom exp3acom23
        zcn expr rexlimdvv mpd modval eqcomd simplbi2com ccnv csup cuz wss crab
        ex c1 ssrab2 eqsstri nnuz sseqtri infmssuzle mpan syl5eqbr syl6 mtod wo
        elnn0 ord divides3 mpbird ) AGIOZHGUEUFZAUUGUGZUUHGHUHPZUIQZUUIUUJUJOZU
        LUUKUUIUULHUUJUKUFZUUIUUJHUMUFZUUMULZUUIGUNOZHUOOZUUNUUIGUJOZUUPUUIUURG
        EUAUPZRPZFUBUPZRPZSPZQZUBTUQZUATUQZUUIUUGUURUVFUGAUUGURDUPZEBUPZRPZFCUP
        ZRPZSPZQZCTUQZBTUQZUVFDGUJIUVGGQZUVOGUVLQZCTUQZBTUQUVFUVPUVMUVQBCTTUVGG
        UVLUSUTUVRUVEBUATUVHUUSQZUVRGUUTUVKSPZQZCTUQUVEUVSUVQUWACTUVSUVLUVTGUVS
        UVIUUTUVKSUVHUUSERVEVAVBVCUWAUVDCUBTUVJUVAQZUVTUVCGUWBUVKUVBUUTSUVJUVAF
        RVEVDVBVPVFVPVFJVGVHZVIZGVJVKZAUUQUUGAHUJOZUUQAUWFHEUCUPZRPZFUDUPZRPZSP
        ZQZUDTUQUCTUQZAHIOUWFUWMUGABCDEFHIJKLMNVLUVOUWMDHUJIUVOUVGUWKQZUDTUQZUC
        TUQUVGHQZUWMUVNUWOBUCTUVHUWGQZUVNUVGUWHUVKSPZQZCTUQUWOUWQUVMUWSCTUWQUVL
        UWRUVGUWQUVIUWHUVKSUVHUWGERVEVAVBVCUWSUWNCUDTUVJUWIQZUWRUWKUVGUWTUVKUWJ
        UWHSUVJUWIFRVEVDVBVPVFVPUWPUWNUWLUCUDTTUVGHUWKUSUTVMJVGVHZVIZHVNVKVOZGH
        VQVRUUIUUJUNOZHUNOZUUNUUOVSUUIUUJVTOZUXDUUIGTOZUWFUXFUUIUURUXGUWDGWAVKZ
        AUWFUUGUXBVOZGHWBVRZUUJWCVKAUXEUUGAUWFUXEUXBHVJVKVOUUJHWDVRWEUUIUULUUJI
        OZUUMUUIUUJUVLQZCTUQBTUQZUULUXKWSUUIGHGHWFPZWGWIZRPZWHPZUVLQZCTUQBTUQZU
        XMUUIUVFUXSUUIUURUVFUWCWJUUIUVDUXSUAUBTTUUIUUSTOZUVATOZUGZUVDUXSWSZUUIU
        YBUGZUWMUYCAUWMUUGUYBAUWFUWMUXAWJWKUYDUWLUYCUCUDTTUUIUYBUWGTOZUWITOZUGZ
        UWLUYCWSUUIUYBUYGUGZUGZUVDUWLUXSUYIUXSUVDUWLUGZUVCUWKUXORPZWHPZUVLQZCTU
        QBTUQZUYIUUSUWGUXORPZWHPZTOZUVAUWIUXORPZWHPZTOZUYLEUYPRPZFUYSRPZSPZQZUY
        NUYIUXTUYOTOZUYQUUIUXTUYAUYGWLZUYIUYEUXOTOZVUEUUIUYBUYEUYFWMZUUIVUGUYHU
        UIUXNUNOZVUGUUIUUPUWFVUIUWEUXIGHWNVRUXNWOVKZVOZUWGUXOWPVRZUUSUYOWTVRUYI
        UYAUYRTOZUYTUUIUXTUYAUYGWQZUYIUYFVUGVUMUUIUYBUYEUYFWRZVUKUWIUXOWPVRZUVA
        UYRWTVRUYIUVCEUYORPZFUYRRPZSPZWHPZUUTVUQWHPZUVBVURWHPZSPZUYLVUCUYIUUTXA
        OZUVBXAOZVUQXAOZVURXAOZVUTVVCQUYIEXAOZUUSXAOZVVDAVVHUUGUYHAETOVVHKEXSVK
        WKZUYIUXTVVIVUFUUSXSVKZEUUSXBVRUYIFXAOZUVAXAOZVVEAVVLUUGUYHAFTOVVLLFXSV
        KWKZUYIUYAVVMVUNUVAXSVKZFUVAXBVRUYIVVHUYOXAOZVVFVVJUYIVUEVVPVULUYOXSVKZ
        EUYOXBVRUYIVVLUYRXAOZVVGVVNUYIVUMVVRVUPUYRXSVKZFUYRXBVRUUTUVBVUQVURXCXD
        UYIUYKVUSUVCWHUYIUYKUWHUXORPZUWJUXORPZSPZVUSUYIUWHXAOZUWJXAOZUXOXAOZUYK
        VWBQUYIVVHUWGXAOZVWCVVJUYIUYEVWFVUHUWGXSVKZEUWGXBVRUYIVVLUWIXAOZVWDVVNU
        YIUYFVWHVUOUWIXSVKZFUWIXBVRUUIVWEUYHUUIVUGVWEVUJUXOXSVKVOZUWHUWJUXOXEXF
        UYIVVTVUQVWAVURSUYIVVHVWFVWEVVTVUQQVVJVWGVWJEUWGUXOXGXFUYIVVLVWHVWEVWAV
        URQVVNVWIVWJFUWIUXOXGXFXHXIVDUYIVUAVVAVUBVVBSUYIVVHVVIVVPVUAVVAQVVJVVKV
        VQEUUSUYOXJXFUYIVVLVVMVVRVUBVVBQVVNVVOVVSFUVAUYRXJXFXHXKUYMVUDUYLVUAUVK
        SPZQBCUYPUYSTTUVHUYPQZUVLVWKUYLVWLUVIVUAUVKSUVHUYPERVEVAVBUVJUYSQZVWKVU
        CUYLVWMUVKVUBVUASUVJUYSFRVEVDVBXLXFUYJUXRUYMBCTTUYJUXQUYLUVLUWLUVDUXPUY
        KQUXQUYLQHUWKUXORXMGUVCUXPUYKWHXNXOXPUTXQXRXTYAYBYKYAYBUUIUXRUXLBCTTUUI
        UXQUUJUVLUUIUUJUXQUUIUUPUUQUUJUXQQUWEUXCGHYCVRYDXPUTWEUXKUULUXMUVOUXMDU
        UJUJIUVGUUJQUVMUXLBCTTUVGUUJUVLUSUTJVGYEVKUXKHIUNUMYFYGZUUJUKMIYLYHWIZY
        IUXKVWNUUJUKUFIUJVWOIUVODUJYJUJJUVODUJYMYNYOYPUUJIYLYQYRYSYTUUAUUIUULUU
        KUUIUXFUULUUKUUBUXJUUJUUCVHUUDYBUUIUWFUXGUUHUUKVSUXIUXHHGUUEVRUUFYK $.
        $( [22-Feb-2014] $)

      $( Lemma for ~ bezout . $)
      bezoutlem4 $p |- ( ph -> ( A gcd B ) e. M ) $=
        ( co wceq wcel cz syl2anc cmul cdivides vu vv vs vt cgcd cle wbr cr cn0
        wa wb gcdcl nn0re syl cn caddc wrex bezoutlem2 weq oveq2 oveq1d rexbidv
        cv eqeq2d oveq2d cbvrexv syl6bb eqeq1 2rexbidv syl5bb elrab2 sylib nnre
        simpld letri3 gcddvds nn0z divides simprd reeanv simprrl simprll zmulcl
        mpbid wi simprrr simprlr zaddcl adantr dvdsmul2 cc adddir syl3anc mul32
        zcn oveq12d eqtrd breqtrd oveq1 oveqan12d breq2d syl5ibcom breq2 imbi2d
        syl5ibrcom expr ex rexlimdvv mpd syl5bir mp2and dvdsle cc0 wne cabs cfv
        com23 bezoutlem1 bezoutlem3 syld nnz dvdsabsb sylibrd imp 3syl pm2.61ne
        dvds0 crab eqid rexcom ad2antll mulcl ad2antrl addcom 2rexbidva rabbidv
        syl5eq eleq2d wn dvdslegcd syl31anc mpbir2and eqeltrd ) AEFUENZGHAUUDGO
        ZUUDGUFUGZGUUDUFUGZAUUDUHPZGUHPZUUEUUFUUGUJUKAUUDUIPZUUHAEQPZFQPZUUJJKE
        FULRZUUDUMUNAGUOPZUUIAUUNGEUAVCZSNZFUBVCZSNZUPNZOZUBQUQUAQUQZAGHPUUNUVA
        UJABCDEFGHIJKLMURZDVCZEBVCZSNZFCVCZSNZUPNZOZCQUQZBQUQZUVADGUOHUVKUVCUUS
        OZUBQUQZUAQUQUVCGOZUVAUVJUVMBUAQBUAUSZUVJUVCUUPUVGUPNZOZCQUQUVMUVOUVIUV
        QCQUVOUVHUVPUVCUVOUVEUUPUVGUPUVDUUOESUTVAVDVBUVQUVLCUBQCUBUSZUVPUUSUVCU
        VRUVGUURUUPUPUVFUUQFSUTVEVDVFVGVFUVNUVLUUTUAUBQQUVCGUUSVHVIVJIVKVLZVNZG
        VMUNUUDGVORAUUDGTUGZUUFAUCVCZUUDSNZEOZUCQUQZUDVCZUUDSNZFOZUDQUQZUWAAUUD
        ETUGZUWEAUWJUUDFTUGZAUUKUULUWJUWKUJJKEFVPRZVNAUUDQPZUUKUWJUWEUKAUUJUWMU
        UMUUDVQUNZJUCUUDEVRRWDAUWKUWIAUWJUWKUWLVSAUWMUULUWKUWIUKUWNKUDUUDFVRRWD
        UWEUWIUJUWDUWHUJZUDQUQUCQUQAUWAUWDUWHUCUDQQVTAUWOUWAUCUDQQAUVAUWBQPZUWF
        QPZUJZUWOUWAWEZWEZAUUNUVAUVSVSAUUTUWTUAUBQQAUUOQPZUUQQPZUJZUUTUWTWEAUXC
        UJUWRUUTUWSAUXCUWRUUTUWSWEAUXCUWRUJZUJZUWSUUTUWOUUDUUSTUGZWEUXEUUDUWCUU
        OSNZUWGUUQSNZUPNZTUGUWOUXFUXEUUDUWBUUOSNZUWFUUQSNZUPNZUUDSNZUXITUXEUXLQ
        PZUWMUUDUXMTUGUXEUXJQPZUXKQPZUXNUXEUWPUXAUXOAUXCUWPUWQWAZAUXAUXBUWRWBZU
        WBUUOWCRZUXEUWQUXBUXPAUXCUWPUWQWFZAUXAUXBUWRWGZUWFUUQWCRZUXJUXKWHRAUWMU
        XDUWNWIZUXLUUDWJRUXEUXMUXJUUDSNZUXKUUDSNZUPNZUXIUXEUXJWKPZUXKWKPZUUDWKP
        ZUXMUYFOUXEUXOUYGUXSUXJWOUNUXEUXPUYHUYBUXKWOUNUXEUWMUYIUYCUUDWOUNZUXJUX
        KUUDWLWMUXEUYDUXGUYEUXHUPUXEUWBWKPZUUOWKPZUYIUYDUXGOUXEUWPUYKUXQUWBWOUN
        UXEUXAUYLUXRUUOWOUNUYJUWBUUOUUDWNWMUXEUWFWKPZUUQWKPZUYIUYEUXHOUXEUWQUYM
        UXTUWFWOUNUXEUXBUYNUYAUUQWOUNUYJUWFUUQUUDWNWMWPWQWRUWOUXIUUSUUDTUWDUWHU
        XGUUPUXHUURUPUWCEUUOSWSUWGFUUQSWSWTXAXBUUTUWAUXFUWOGUUSUUDTXCXDXEXFXQXG
        XHXIXHXJXKAUWMUUNUWAUUFWEUWNUVTUUDGXLRXIAGETUGZGFTUGZUUGAUYOGXMTUGZEXME
        XMGTXCAEXMXNZUYOAUYRGEXOXPZTUGZUYOAUYRUYSHPUYTABCDEFHIJKXRABCDEFUYSGHIJ
        KLMXSXTAGQPZUUKUYOUYTUKAUUNVUAUVTGYAZUNZJGEYBRYCYDAUUNVUAUYQUVTVUBGYGYE
        ZYFAUYPUYQFXMFXMGTXCAFXMXNZUYPAVUEGFXOXPZTUGZUYPAVUEVUFHPZVUGAVUEVUFUVC
        UVGUVEUPNZOZBQUQCQUQZDUOYHZPVUHACBDFEVULVULYIKJXRAHVULVUFAHUVKDUOYHVULI
        AUVKVUKDUOUVKUVIBQUQCQUQAVUKUVIBCQQYJAUVIVUJCBQQAUVFQPZUVDQPZUJZUJZUVHV
        UIUVCVUPUVEWKPZUVGWKPZUVHVUIOVUPEWKPZUVDWKPZVUQAVUSVUOAUUKVUSJEWOUNWIVU
        NVUTAVUMUVDWOYKEUVDYLRVUPFWKPZUVFWKPZVURAVVAVUOAUULVVAKFWOUNWIVUMVVBAVU
        NUVFWOYMFUVFYLRUVEUVGYNRVDYOVJYPYQYRYCABCDEFVUFGHIJKLMXSXTAVUAUULUYPVUG
        UKVUCKGFYBRYCYDVUDYFAVUAUUKUULEXMOFXMOUJYSUYOUYPUJUUGWEVUCJKMGEFYTUUAXK
        UUBUVBUUC $.
        $( [22-Feb-2014] $)
    $}

    $( B&eacute;zout's identity:  For any integers ` A ` and ` B ` , there are
       integers ` x , y ` such that ` ( A gcd B ) = A x. x + B x. y ` .
       (Contributed by Mario Carneiro, 22-Feb-2014.) $)
    bezout $p |- ( ( A e. ZZ /\ B e. ZZ ) ->
           E. x e. ZZ E. y e. ZZ ( A gcd B ) = ( ( A x. x ) + ( B x. y ) ) ) $=
      ( vz vu vv vt cz wcel cc0 wceq co cv cmul caddc wrex cn oveq2 eqeq2d cgcd
      wa wn crab clt ccnv csup weq eqeq1 2rexbidv oveq1d rexbidv oveq2d cbvrexv
      cr syl6bb cbvrabv simpll simplr eqid simpr bezoutlem4 elrab simprbi ex 0z
      syl 00id 0cn mul01i oveq12i gcd0val 3eqtr4ri mp3an oveq12 oveq1 oveqan12d
      rcla42ev eqeq12d mpbiri pm2.61d2 ) CIJZDIJZUBZCKLZDKLZUBZCDUAMZCANZOMZDBN
      ZOMZPMZLZBIQAIQZWDWGUCZWOWDWPUBZWHENZWMLZBIQAIQZERUDZJZWOWQFGHCDXAUOUEUFU
      GZXAWTHNZCFNZOMZDGNZOMZPMZLZGIQZFIQZEHREHUHZWTXDWMLZBIQZAIQXLXMWSXNABIIWR
      XDWMUIUJXOXKAFIAFUHZXOXDXFWLPMZLZBIQXKXPXNXRBIXPWMXQXDXPWJXFWLPWIXECOSUKT
      ULXRXJBGIBGUHZXQXIXDXSWLXHXFPWKXGDOSUMTUNUPUNUPUQWBWCWPURWBWCWPUSXCUTWDWP
      VAVBXBWHRJWOWTWOEWHRWRWHLWSWNABIIWRWHWMUIUJVCVDVGVEWGWOKKUAMZKWIOMZKWKOMZ
      PMZLZBIQAIQZKIJZYFXTKKOMZYGPMZLZYEVFVFKKPMKYHXTVHYGKYGKPKVIVJZYJVKVLVMYDY
      IXTYGYBPMZLABKKIIWIKLZYCYKXTYLYAYGYBPWIKKOSUKTWKKLZYKYHXTYMYBYGYGPWKKKOSU
      MTVRVNWGWNYDABIIWGWHXTWMYCCKDKUAVOWEWFWJYAWLYBPCKWIOVPDKWKOVPVQVSUJVTWA
      $.
      $( [22-Feb-2014] $)
  $}

  ${
    $d K x y $.  $d M x y $.  $d N x y $.
    $( An integer which divides each of two others also divides their gcd.
       (Contributed by Paul Chapman, 22-Jun-2011.)  (Revised by Mario Carneiro,
       30-May-2014.) $)
    dvdsgcd $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K || M /\ K || N ) -> K || ( M gcd N ) ) ) $=
      ( vx vy cz wcel w3a cdivides wbr wa co cv cmul caddc wceq wrex wi cc zcn
      bezout 3adant1 dvds2ln 3impia simp3l simp12 mulcom syl2an syl2anc oveq12d
      cgcd 3coml simp3r simp13 breqtrd breq2 syl5ibrcom 3expia rexlimdvv mpid
      ex ) AFGZBFGZCFGZHZABIJACIJKZBCUKLZBDMZNLZCEMZNLZOLZPZEFQDFQZAVGIJZVCVDVN
      VBDEBCUAUBVEVFVNVORVEVFKVMVODEFFVEVFVHFGZVJFGZKZVMVORVEVFVRHZVOVMAVLIJVSA
      VHBNLZVJCNLZOLZVLIVRVEVFAWBIJZVRVEVFWCVHVJABCUCUDULVSVTVIWAVKOVSVPVCVTVIP
      ZVEVFVPVQUEVBVCVDVFVRUFVPVHSGBSGWDVCVHTBTVHBUGUHUIVSVQVDWAVKPZVEVFVPVQUMV
      BVCVDVFVRUNVQVJSGCSGWEVDVJTCTVJCUGUHUIUJUOVGVLAIUPUQURUSVAUT $.
      $( [30-May-2014] $) $( [22-Jun-2011] $)
  $}

  $( Distribute multiplication by a nonnegative integer over gcd.  (Contributed
     by Paul Chapman, 22-Jun-2011.)  (The proof was shortened by Mario
     Carneiro, 30-May-2014.) $)
  mulgcd $p |- ( ( K e. NN0 /\ M e. ZZ /\ N e. ZZ ) ->
                 ( ( K x. M ) gcd ( K x. N ) ) = ( K x. ( M gcd N ) ) ) $=
    ( cn0 wcel cz cmul co cgcd wceq cc0 wa cdivides wbr syl2anc 3adant1 syl3anc
    wi syl cc cn wo elnn0 w3a simp1 nnz simp2 zmulcl simp3 gcdcl nnnn0 nn0mulcl
    cdiv wne nn0cn nncn nnne0 divcan2 gcddvds simpld eqbrtrd wb dvdsmul1 mp2and
    nn0z divides2 mpbid dvdscmulr syl112anc simprd dvdscmul mpd eqbrtrrd dvdseq
    dvdsgcd syl22anc 3expib mul02 gcd0val syl6reqr oveq1d zcn 3ad2ant2 3ad2ant3
    eqtrd oveq12d 3eqtr4d jaoi sylbi 3impib ) ADEZBFEZCFEZABGHZACGHZIHZABCIHZGH
    ZJZWKAUAEZAKJZUBWLWMLWSRZAUCWTXBXAWTWLWMWSWTWLWMUDZWPDEZWRDEZWPWRMNWRWPMNZW
    SXCWNFEZWOFEZXDXCAFEZWLXGXCWTXIWTWLWMUEZAUFSZWTWLWMUGZABUHOZXCXIWMXHXKWTWLW
    MUIZACUHOZWNWOUJOZXCWKWQDEZXEXCWTWKXJAUKSWLWMXQWTBCUJZPZAWQULOZXCAWPAUMHZGH
    ZWPWRMXCWPTEZATEZAKUNZYBWPJXCXDYCXPWPUOSXCWTYDXJAUPSXCWTYEXJAUQSZWPAURQZXCY
    AWQMNZYBWRMNZXCYABMNZYACMNZYHXCYBWNMNZYJXCYBWPWNMYGXCWPWNMNZWPWOMNZXCXGXHYM
    YNLXMXOWNWOUSOZUTVAXCYAFEZWLXIYEYLYJVBXCAWPMNZYPXCAWNMNZAWOMNZYQXCXIWLYRXKX
    LABVCOXCXIWMYSXKXNACVCOXCXIXGXHYRYSLYQRXKXMXOAWNWOVOQVDXCXIYEWPFEZYQYPVBXKY
    FXCXDYTXPWPVESAWPVFQVGZXLXKYFAYABVHVIVGXCYBWOMNZYKXCYBWPWOMYGXCYMYNYOVJVAXC
    YPWMXIYEUUBYKVBUUAXNXKYFAYACVHVIVGXCYPWLWMYJYKLYHRUUAXLXNYABCVOQVDXCYPWQFEZ
    XIYHYIRUUAXCXQUUCXSWQVESZXKAYAWQVKQVLVMXCWRWNMNZWRWOMNZXFXCWQBMNZUUEXCUUGWQ
    CMNZWLWMUUGUUHLWTBCUSPZUTXCUUCWLXIUUGUUERUUDXLXKAWQBVKQVLXCUUHUUFXCUUGUUHUU
    IVJXCUUCWMXIUUHUUFRUUDXNXKAWQCVKQVLXCWRFEZXGXHUUEUUFLXFRXCXEUUJXTWRVESXMXOW
    RWNWOVOQVDWPWRVNVPVQXAWLWMWSXAWLWMUDZKKIHZKWQGHZWPWRUUKUUMKUULUUKWQTEZUUMKJ
    UUKXQUUNWLWMXQXAXRPWQUOSWQVRSVSVTUUKWNKWOKIUUKWNKBGHZKUUKAKBGXAWLWMUEZWAUUK
    BTEZUUOKJWLXAUUQWMBWBWCBVRSWEUUKWOKCGHZKUUKAKCGUUPWAUUKCTEZUURKJWMXAUUSWLCW
    BWDCVRSWEWFUUKAKWQGUUPWAWGVQWHWIWJ $.
    $( [30-May-2014] $) $( [22-Jun-2011] $)

  $( Distribute absolute value of multiplication over gcd.  (Contributed by
     Paul Chapman, 22-Jun-2011.) $)
  absmulgcd $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
      ( ( K x. M ) gcd ( K x. N ) ) = ( abs ` ( K x. ( M gcd N ) ) ) ) $=
    ( cz wcel cabs cfv cgcd co cmul wceq wa cn0 syl oveq2d 3adant1 zcn nn0abscl
    cc absmul w3a gcdcl cc0 cle wbr nn0re nn0ge0 absid syl2anc syl2an oveqan12d
    nn0cn 3impb 3impdi syl3an zmulcl gcdabs nn0z mulgcd 3eqtr3d eqtrd 3eqtr4rd
    cr ) ADEZBDEZCDEZUAZAFGZBCHIZFGZJIZVHVIJIZAVIJIFGZABJIZACJIZHIZVEVFVKVLKVDV
    EVFLZVJVIVHJVQVIMEZVJVIKZBCUBZVRVIVCEUCVIUDUEVSVIUFVIUGVIUHUINOPVDVEVFVMVKK
    ZVDASEZVISEZWAVQAQZVQVRWCVTVIULNAVITUJUMVGVPVHBFGZCFGZHIZJIZVLVGVNFGZVOFGZH
    IZVHWEJIZVHWFJIZHIZVPWHVDWBVEBSEZVFCSEZWKWNKZWDBQCQWBWOWPWQWBWOLWBWPLWIWLWJ
    WMHABTACTUKUNUOVDVEVFWKVPKZVDVELVNDEVODEWRVDVFLABUPACUPVNVOUQUJUNVDVHMEVEWE
    DEZVFWFDEZWNWHKARVEWEMEWSBRWEURNVFWFMEWTCRWFURNVHWEWFUSUOUTVGWGVIVHJVEVFWGV
    IKVDBCUQPOVAVB $.
    $( [22-Jun-2011] $)

