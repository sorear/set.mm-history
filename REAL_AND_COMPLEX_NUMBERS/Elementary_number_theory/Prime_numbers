$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                                Prime numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Prime $.

  $( Extend the definition of a class to include the set of prime numbers. $)
  cprime $a class Prime $.

  ${
    $d n p $.
    $( Define the set of prime numbers. $)
    df-prime $a |- Prime = { p e. NN | { n e. NN | n || p } ~~ 2o } $.
  $}

  ${
    $d P n p z $.
    $( The predicate "is a prime number".  A prime number is a positive integer
       with exactly two positive divisors.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    isprm $p |- ( P e. Prime <->
                  ( P e. NN /\ { n e. NN | n || P } ~~ 2o ) ) $=
      ( vp cv cdivides wbr cn crab c2o cen cprime wceq wb breq2 adantr rabbidva
      wcel breq1d df-prime elrab2 ) BDZCDZEFZBGHZIJFUAAEFZBGHZIJFCAGKUBALZUDUFI
      JUGUCUEBGUGUCUEMUAGQUBAUAENOPRBCST $.
      $( [22-Jun-2011] $)


    $( A prime number is an integer.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    prmz $p |- ( P e. Prime -> P e. ZZ ) $=
      ( vn cprime wcel cn cz cv cdivides wbr crab c2o cen isprm simplbi nnz syl
      ) ACDZAEDZAFDQRBGAHIBEJKLIABMNAOP $.
      $( [23-Nov-2012] $) $( [22-Jun-2011] $)

    $( A prime number is a positive integer.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    prmnn $p |- ( P e. Prime -> P e. NN ) $=
      ( vz cprime wcel cn cv cdivides wbr crab c2o cen wa isprm biimpi simpld )
      ACDZAEDZBFAGHBEIJKHZPQRLABMNO $.
      $( [22-Jun-2011] $) $( [26-Jun-2011] $)
  $}

  ${
    $d n z $.
    $( 1 is not a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    1nprm $p |- 1 e/ Prime $=
      ( vn vz c1 cprime wcel wn cv cdivides wbr cn c2o cen csdm c1o wa cz mpan2
      cle 1nn syl wnel crab csn wceq wi nnz dvdsle nnge1 jcad cr wb nnre letri3
      a1d 1re sylibrd imp iddvds ax-mp pm3.2i eleq1 breq1 anbi12d mpbiri impbii
      elrab elsn 3bitr4i eqriv elexi ensn1 eqbrtri ensdomtr mp2an sdomnen isprm
      1z 1sdom2 mpbiran mtbir df-nel mpbir ) CDUACDEZFWCAGZCHIZAJUBZKLIZWFKMIZW
      GFWFNLINKMIWHWFCUCZNLBWFWIBGZJEZWJCHIZOZWJCUDZWJWFEWJWIEWMWNWKWLWNWKWLWJC
      RIZCWJRIZOZWNWKWLWOWPWKWJPEZWLWOUEZWJUFWRCJEZWSSWJCUGQTWKWPWLWJUHUNUIWKWJ
      UJEZWNWQUKZWJULXACUJEXBUOWJCUMQTUPUQWNWMWTCCHIZOWTXCSCPEXCVQCURUSUTWNWKWT
      WLXCWJCJVAWJCCHVBVCVDVEWEWLAWJJWDWJCHVBVFBCVGVHVICCJSVJVKVLVRWFNKVMVNWFKV
      OUSWCWTWGCAVPSVSVTCDWAWB $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d N n $.
    $( The positive divisors of a positive integer include 1 and itself.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    1idssfct $p |- ( N e. NN -> { 1 , N } C_ { n e. NN | n || N } ) $=
      ( cn wcel c1 cv cdivides wbr crab cpr wss cz nnz 1dvds 1nn wa breq1 elrab
      biimpri mpan 3syl iddvds syl mpdan prssg ibi syl2anc ) BCDZEAFZBGHZACIZDZ
      BUKDZEBJUKKZUHBLDZEBGHZULBMZBNECDZUPULOULURUPPUJUPAECUIEBGQRSTUAUHBBGHZUM
      UHUOUSUQBUBUCUMUHUSPUJUSABCUIBBGQRSUDULUMPUNEBUKUKUKUEUFUG $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d P n p $.
    $( Lemma for ~ isprm2 . $)
    isprm2lem $p |- ( ( P e. NN /\ P =/= 1 ) ->
      ( { n e. NN | n || P } ~~ 2o <-> { n e. NN | n || P } = { 1 , P } ) ) $=
      ( vp cn wcel c1 wne cv cdivides wbr crab c2o cen cpr wceq wb wi wa wn ex
      neeq1 breq2 rabbidv breq1d preq2 eqeq12d bibi12d imbi12d wss 1idssfct csn
      cun cin c0 disjsn c1o 1nn elexi ensn1 pm54.43 mp2an elsnc notbii 3bitr3ri
      vex df-ne df-pr breq1i 3bitr4i com 2onn ensym entr sylan2 sylanb cfn prfi
      nnex ssrab2 ssexi enfi mpan mpbii adantl wpss dfpss2 pssinf sylanbr an32s
      cvv con4d mpd syl2an eqcomd anassrs breq1 syl6bbr biimprcd impbid vtoclri
      rgen imp ) ADEAFGZBHZAIJZBDKZLMJZXFFANZOZPZCHZFGZXDXKIJZBDKZLMJZXNFXKNZOZ
      PZQZXCXJQCADXKAOZXLXCXRXJXKAFUAXTXOXGXQXIXTXNXFLMXTXMXEBDXKAXDIUBUCZUDXTX
      NXFXPXHYAXKAFUEUFUGUHXSCDXKDEZXLXRYBXLRZXOXQYCXOXQYBXLXOXQYBXLXORZRXPXNYB
      XPXNUIZXPXNMJZXPXNOZYDBXKUJXLXPLMJZXOYFXKFOZSZFUKZXKUKZULZLMJZXLYHYKYLUMU
      NOZXKYKEZSYNYJYKXKUOYKUPMJYLUPMJYOYNPFFDUQURUSXKCVEZUSYKYLUTVAYPYIXKFYQVB
      VCVDXKFVFXPYMLMFXKVGVHVIZXOYHLXNMJYFXNLLVJVKURVLXPLXNVMVNVOYEYFRZXNVPEZYG
      YFYTYEYFXPVPEZYTFXKVQXNWJEYFUUAYTPXNDVRXMBDVSVTXPXNWJWAWBWCWDYSYGYTYSYGSZ
      YTSZYEUUBYFUUCYEUUBRXPXNWEYFUUCXPXNWFXPXNWGWHWITWKWLWMWNWOTXLXQXOQYBXQXOX
      LXQXOYHXLXNXPLMWPYRWQWRWDWSTXAWTXB $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d P n z $.
    $( The predicate "is a prime number".  A prime number is an integer greater
       than or equal to 2 whose only positive divisors are 1 and itself.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    isprm2 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                      A. z e. NN ( z || P -> ( z = 1 \/ z = P ) ) ) ) $=
      ( vn cprime wcel cn cv cdivides wbr c1 wss wa wceq wi wn wb 3bitr4i bitri
      ancom wal crab cpr wne c2 cuz cfv wo wral wnel 1nprm df-nel eleq1 biimpcd
      mpbi mtoi df-ne sylibr pm4.71i c2o cen isprm isprm2lem jcab eqss 1idssfct
      imbi2i biantru pm5.74ri adantr bitrd expcom pm5.32d syl5bb pm5.32ri anass
      eluz2b3 bitr4i anbi1i 3bitr2i breq1 elrab elpr imbi12i impexp albii dfss2
      vex df-ral anbi2i 3bitri ) BDEZBFEZCGZBHIZCFUAZJBUBZKZLZBJUCZLZBUDUEUFEZW
      QLZXAAGZBHIZXCJMXCBMUGZNZAFUHZLWKWKWSLWTWKWSWKBJMZOWSWKXHJDEZJDUIXIOUJJDU
      KUNXHWKXIBJDULUMUOBJUPUQURWSWKWRWKWLWOUSUTIZLWSWRBCVAWSWLXJWQWLWSXJWQPWLW
      SLZXJWOWPMZWQBCVBWLXLWQPWSWLXLWQWLWQWPWOKZLZNWLWQNZWLXMNZLWLXLNXOWLWQXMVC
      XLXNWLWOWPVDVFXPXOCBVEVGQVHVIVJVKVLVMVNRWTWSWRLWSWLLZWQLXBWRWSSWSWLWQVOXQ
      XAWQXQXKXAWSWLSBVPVQVRVSWQXGXAXCWOEZXCWPEZNZATXCFEZXFNZATWQXGXTYBAXTYAXDL
      ZXENYBXRYCXSXEWNXDCXCFWMXCBHVTWAXCJBAWGWBWCYAXDXEWDRWEAWOWPWFXFAFWHQWIWJ
      $.
      $( [23-Nov-2012] $) $( [22-Jun-2011] $)

    $( The predicate "is a prime number".  A prime number is an integer greater
       than or equal to 2 with no divisors strictly between 1 and itself.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    isprm3 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                      A. z e. ( 2 ... ( P - 1 ) ) -. z || P ) ) $=
      ( wcel c2 cuz cfv wbr c1 wi cn wa wn co wb cle adantr cz wne cr syl2an cv
      cprime cdivides wceq wral cmin cfz isprm2 bi2.04 iman clt 2nn elnnuz mpbi
      wo uztrn mpan2 sylibr nnge1 a1d nnz dvdsle sylan jcad sylan2 zre nnre 1re
      leltne mp3an1 3adant2 3expia pm4.38 df-ne necom bitri ioran bitr4i bibi2i
      anim12d anbi12i sylib syl6 syld eluzelz caddc 1z zltp1le mpan df-2 breq1i
      imp syl6bbr zltlem1 anbi12d peano2zm 2z elfz mp3an2 bitr4d bitr3d pm5.32d
      anasss expcom fzssuz wss 2re 1lt2 ltleii eluz mp2an mpbir uzss ax-mp nnuz
      sstri sseqtr4i sseli pm4.71ri notbid syl5bb pm5.74d con2b syl6bb ralbidv2
      ex syl5bbr pm5.32i ) BUBCBDEFZCZAUAZBUCGZYKHUDZYKBUDZUOZIZAJUEZKYJYLLZADB
      HUFMZUGMZUEZKABUHYJYQUUAYJYPYRAJYTYJYKJCZYPIZYLYKYTCZLZIZUUDYRIUUCYLUUBYO
      IZIYJUUFYLUUBYOUIYJYLUUGUUEYJYLUUGUUENUUGUUBYOLZKZLYJYLKZUUEUUBYOUJUUJUUI
      UUDUUJUUIUUBUUDKUUDUUJUUBUUHUUDUUBUUJUUHUUDNZUUBYJYLUUKUUBYJKZYLKHYKUKGZY
      KBUKGZKZUUHUUDUULYLUUOUUHNZUULYLHYKOGZYKBOGZKZUUPYJUUBBJCZYLUUSIYJBHEFZCZ
      UUTYJDUVACZUVBDJCUVCULDUMUNDBHUPUQBUMURZUUBUUTKZYLUUQUURUVEUUQYLUUBUUQUUT
      YKUSPUTUUBYKQCZUUTYLUURIYKVAZYKBVBVCVDVEUUBUVFUUTUUSUUPIYJUVGUVDUVFUUTKUU
      SUUMYKHRZNZUUNBYKRZNZKZUUPUVFYKSCZBSCZUUSUVLIUUTYKVFBVGUVMUVNKUUQUVIUURUV
      KUVMUVNUUQUVIUVMUUQUVIUVNHSCUVMUUQUVIVHHYKVIVJVKVLUVMUVNUURUVKYKBVIVLVTTU
      VLUUOUVHUVJKZNUUPUUMUUNUVHUVJVMUVOUUHUUOUVOYMLZYNLZKUUHUVHUVPUVJUVQYKHVNU
      VJYKBRUVQBYKVOYKBVNVPWAYMYNVQVRVSWBWCTWDWLUULUUOUUDNZYLUUBUVFBQCZUVRYJUVG
      DBWEUVFUVSKZUUODYKOGZYKYSOGZKZUUDUVTUUMUWAUUNUWBUVFUUMUWANUVSUVFUUMHHWFMZ
      YKOGZUWAHQCZUVFUUMUWENWGHYKWHWIDUWDYKOWJWKWMPYKBWNWOUVSUVFYSQCZUUDUWCNZBW
      PUVFDQCZUWGUWHWQYKDYSWRWSVEWTTPXAXCXDXBUUDUUBYTJYKYTUVAJYTYIUVADYSXEUVCYI
      UVAXFUVCHDOGZHDVHXGXHXIUWFUWIUVCUWJNWGWQHDXJXKXLHDXMXNXPXOXQXRXSWMXTYAYFY
      BYGYLUUDYCYDYEYHVP $.
      $( [27-Nov-2012] $) $( [22-Jun-2011] $)
  $}

  ${
    $d P z $.
    $( The predicate "is a prime number".  A prime number is an integer greater
       than or equal to 2 whose only divisor greater than or equal to 2 is
       itself.  (Contributed by Paul Chapman, 26-Oct-2012.) $)
    isprm4 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                      A. z e. ( ZZ>= ` 2 ) ( z || P -> z = P ) ) ) $=
      ( cprime wcel c2 cuz cfv cv cdivides wbr c1 wceq wo wi wral imbi1i bitr4i
      cn wa imbi2i isprm2 wne eluz2b3 impexp bi2.04 wn df-ne df-or bitri 3bitri
      ralbii2 anbi2i ) BCDBEFGZDZAHZBIJZUOKLZUOBLZMZNZAROZSUNUPURNZAUMOZSABUAVC
      VAUNVBUTAUMRUOUMDZVBNUORDZUOKUBZSZVBNVEVFVBNZNVEUTNVDVGVBUOUCPVEVFVBUDVHU
      TVEVHUPVFURNZNUTVFUPURUEVIUSUPVIUQUFZURNUSVFVJURUOKUGPUQURUHQTUITUJUKULQ
      $.
      $( [23-Nov-2012] $) $( [26-Oct-2012] $)
  $}

  ${
    $d P x $.
    $( A prime number is an integer greater than or equal to 2.  (Contributed
       by Paul Chapman, 17-Nov-2012.) $)
    prmuz2 $p |- ( P e. Prime -> P e. ( ZZ>= ` 2 ) ) $=
      ( vx cprime wcel c2 cuz cfv cv cdivides wbr wceq wi wral isprm4 simplbi )
      ACDAEFGZDBHZAIJQAKLBPMBANO $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d N z $.  $d P z $.
    $( An integer greater than or equal to 2 divides a prime number iff it is
       equal to it.  (Contributed by Paul Chapman, 26-Oct-2012.) $)
    dvdsprm $p |- ( ( N e. ( ZZ>= ` 2 ) /\ P e. Prime ) ->
                    ( N || P <-> N = P ) ) $=
      ( vz c2 cuz cfv wcel cprime wa cdivides wbr wceq wi cv wral simprbi breq1
      isprm4 eqeq1 imbi12d rcla4v mpan9 ancoms cz iddvds breq2 syl5ibcom adantr
      eluzelz syl impbid ) BDEFZGZAHGZIBAJKZBALZUNUMUOUPMZUNCNZAJKZURALZMZCULOZ
      UMUQUNAULGVBCARPVAUQCBULURBLUSUOUTUPURBAJQURBASTUAUBUCUMUPUOMZUNUMBUDGZVC
      DBUIVDBBJKUPUOBUEBABJUFUGUJUHUK $.
      $( [26-Oct-2012] $)
  $}

  $( 2 is a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  2prm $p |- 2 e. Prime $=
    ( vz c2 cprime wcel cuz cfv cv wbr c1 co wral cz 2z 1lt2 mpbir2an c0 ax-1cn
    clt wceq mpbir cdivides wn cmin cfz isprm3 eluz2b1 ral0 df-2 eqcomi subaddi
    caddc 2cn eqbrtri wb 1z eqeltri fzn mp2an mpbi raleqi ) BCDBBEFDZAGBUAHUBZA
    BBIUCJZUDJZKZABUEVABLDZIBRHBUFMNOVEVBAPKVBAUGVBAVDPVCBRHZVDPSZVCIBRVCISIIUK
    JZBSBVIUHUIBIIULQQUJTZNUMVFVCLDVGVHUNMVCILVJUOUPBVCUQURUSUTTO $.
    $( [27-Nov-2012] $) $( [22-Jun-2011] $)

  $( 3 is a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  3prm $p |- 3 e. Prime $=
    ( vz c3 wcel c2 cdivides wbr wn c1 co cfz cz clt cn 3nn ax-mp mpbir2an wceq
    df-3 wi 2z cprime cuz cfv cv cmin wral isprm3 eluz2b1 nnz 1lt3 caddc eqcomi
    nncn ax-1cn 2cn subadd2i mpbir oveq2i eleq2i iddvds 2nn 1lt2 ndvdsp1 breq2i
    cc mp3an mtbir breq1 mtbiri elfz1eq elfz3 eleq1 mpbiri impbii imbi1i sylbi
    rgen ) BUACBDUBUCCZAUDZBEFZGZADBHUEIZJIZUFABUGVRBKCZHBLFBUHBMCZWDNBUIOUJPWA
    AWCVSWCCVSDDJIZCZWAWCWFVSWBDDJWBDQDHUKIZBQBWHRULBHDWEBVECNBUMOUNUOUPUQURUSW
    GWASVSDQZWASWIVTDBEFZWJDWHEFZDDEFZWKGZDKCZWLTDUTOWNDMCHDLFWLWMSTVAVBDDVCVFO
    BWHDERVDVGVSDBEVHVIWGWIWAWGWIVSDVJWIWGDWFCZWNWOTDVKOVSDWFVLVMVNVOUQVPVQP $.
    $( [23-Nov-2012] $) $( [22-Jun-2011] $)


  ${
    $d N z $.  $d P z $.
    $( A prime number either divides an integer or is coprime to it, but not
       both.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    coprm $p |- ( ( P e. Prime /\ N e. ZZ ) ->
                  ( -. P || N <-> ( P gcd N ) = 1 ) ) $=
      ( vz wcel cz wa cdivides wbr wn c1 wceq clt wo wi cn sylan cc0 adantr syl
      cr cprime cgcd co c2 cuz cfv cv wral isprm2 simplbi eluz2b1 sylib gcddvds
      simpld simprd breq1 syl5ibcom con3d 0nnn prmnn eleq1 intnanrd gcdn0cl mpd
      mtoi simprbi eqeq1 orbi12d imbi12d rcla4v syl5com mp2d biorf orcom syl6bb
      ex syl5ibrcom syld wne cle iddvds w3a dvdslegcd 3anidm12 mpand zre ltletr
      nnre mp3an1 syl2anc ltne 3expia sylancr 3syld df-ne syl6ib con2d impbid
      1re ) AUADZBEDZFZABGHZIZABUBUCZJKZXBXDXEAKZIZXFXBXGXCXBXEBGHZXGXCXBXEAGHZ
      XIWTAEDZXAXJXIFWTXKJALHZWTAUDUEUFDZXKXLFWTXMCUGZAGHZXNJKZXNAKZMZNZCOUHZCA
      UIZUJAUKULZUNZABUMPZUOXEABGUPUQURXBXFXHXFXGMZXBXEODZXJYEXBAQKZBQKZFIZYFWT
      YIXAWTYGYHWTYGQODZUSWTAODYGYJAUTAQOVAUQVEVBRZWTXKXAYIYFNYCXKXAFYIYFABVCVP
      PVDZXBXJXIYDUNWTYFXJYENZNXAWTXTYFYMWTXMXTYAVFXSYMCXEOXNXEKZXOXJXRYEXNXEAG
      UPYNXPXFXQXGXNXEJVGXNXEAVGVHVIVJVKRVLXHXFXGXFMYEXGXFVMXGXFVNVOVQVRXBXCXFX
      BXCXEJVSZXFIXBXCAXEVTHZJXELHZYOXBAAGHZXCYPWTYRXAWTXKYRYCAWASRXBYIYRXCFYPN
      ZYKWTXKXAYIYSNZYCXKXAYTXKXKXAWBYIYSAABWCVPWDPVDWEXBXLYPYQWTXLXAWTXKXLYBUO
      RXBATDZXETDZXLYPFYQNZWTUUAXAWTXKUUAYCAWFSRXBYFUUBYLXEWHSZJTDZUUAUUBUUCWSJ
      AXEWGWIWJWEXBUUEUUBYQYONWSUUDUUEUUBYQYOJXEWKWLWMWNXEJWOWPWQWR $.
      $( [23-Nov-2012] $) $( [22-Jun-2011] $)
  $}

  $( If an integer divides the product of two integers and is coprime to one of
     them, then it divides the other.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  coprmdvds $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                    ( ( K || ( M x. N ) /\ ( K gcd M ) = 1 ) -> K || N ) ) $=
    ( cz wcel cmul co cdivides wbr cgcd c1 wceq wi wa zcn ancoms 3adant2 adantr
    cc cabs w3a mulcom syl2an 3adant1 breq2d simp1 zmulcl dvdsgcd syl3anc mpand
    dvdsmul2 sylbid cfv absmulgcd 3coml oveq2 mulid1 sylan9eqr fveq2d 3ad2antl3
    syl eqtrd wb dvdsabsb bitr4d sylibd ex com23 imp3a ) ADEZBDEZCDEZUAZABCFGZH
    IZABJGZKLZACHIZVMVQVOVRVMVQVOVRMVMVQNZVOACAFGZCBFGZJGZHIZVRVMVOWCMVQVMVOAWA
    HIZWCVMVNWAAHVKVLVNWALZVJVKBSECSEZWEVLBOCOZBCUBUCUDUEVMAVTHIZWDWCVJVLWHVKVL
    VJWHCAUKPQVMVJVTDEZWADEZWHWDNWCMVJVKVLUFVJVLWIVKVLVJWICAUGPQVKVLWJVJVLVKWJC
    BUGPUDAVTWAUHUIUJULRVSWCACTUMZHIZVRVSWBWKAHVSWBCVPFGZTUMZWKVMWBWNLZVQVLVJVK
    WOCABUNUORVLVJVQWNWKLVKVLVQNWMCTVQVLWMCKFGZCVPKCFUPVLWFWPCLWGCUQVAURUSUTVBU
    EVMVRWLVCZVQVJVLWQVKACVDQRVEVFVGVHVI $.
    $( [22-Jun-2011] $)
  ${
    $d x K $.  $d x M $.  $d x N $.
    $( If an integer is divisible by two coprime integers, than it is divisible
       by their product.  (Contributed by Mario Carneiro, 24-Feb-2014.) $)
    coprmdvds2 $p |- ( ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\
                         ( M gcd N ) = 1 ) ->
                       ( ( M || K /\ N || K ) -> ( M x. N ) || K ) ) $=
      ( vx cz wcel w3a cgcd co wceq wa cdivides wbr cmul wi cc zcn sylbid breq2
      syl3anc c1 cv wrex wb divides 3adant1 adantr simprr simpl2 mulcom syl2anc
      syl2an breq2d simprl simpl1 coprmdvds mpan2d dvdsmulc syl5ibcom rexlimdva
      syld imbi12d anassrs com23 imp3a ) BEFZCEFZAEFZGZBCHIUAJZKZBALMZCALMZBCNI
      ZALMZVKVMVLVOVKVMDUBZCNIZAJZDEUCZVLVOOZVIVMVSUDZVJVGVHWAVFDCAUEUFUGVKVRVT
      DEVIVJVPEFZVRVTOVIVJWBKZKZBVQLMZVNVQLMZOVRVTWDWEBVPLMZWFWDWEBCVPNIZLMZWGW
      DVQWHBLWDWBVGVQWHJZVIVJWBUHZVFVGVHWCUIZWBVPPFCPFWJVGVPQCQVPCUJULUKUMWDWIV
      JWGVIVJWBUNWDVFVGWBWIVJKWGOVFVGVHWCUOZWLWKBCVPUPTUQRWDVFWBVGWGWFOWMWKWLCB
      VPURTVAVRWEVLWFVOVQABLSVQAVNLSVBUSVCUTRVDVE $.
      $( [24-Feb-2014] $)
  $}

  $( Euclid's lemma.  A prime number divides the product of two integers iff it
     divides at least one of them.  (Contributed by Paul Chapman,
     17-Nov-2012.) $)
  euclemma $p |- ( ( P e. Prime /\ M e. ZZ /\ N e. ZZ ) ->
                        ( P || ( M x. N ) <-> ( P || M \/ P || N ) ) ) $=
    ( cprime wcel cz w3a cmul co cdivides wbr wo wn wi wa cgcd c1 wceq syl3an1
    wb coprm 3adant3 anbi2d prmz coprmdvds sylbid exp3a df-or syl6ibr ordvdsmul
    impbid ) ADEZBFEZCFEZGZABCHIJKZABJKZACJKZLZUOUPUQMZURNUSUOUPUTURUOUPUTOUPAB
    PIQRZOZURUOUTVAUPULUMUTVATUNABUAUBUCULAFEZUMUNVBURNAUDZABCUESUFUGUQURUHUIUL
    VCUMUNUSUPNVDABCUJSUK $.
    $( [17-Nov-2012] $)

  ${
    $d N m $.  $d S x y $.  $d m x y $.
    exprmfctlem.1 $e |- S = { m e. ( ZZ>= ` 2 ) | m || N } $.
    exprmfctlem.2 $e |- N e. ( ZZ>= ` 2 ) $.
    $( Lemma for ~ exprmfct . $)
    exprmfctlem $p |- E. x e. Prime x || N $=
      ( vy cv wbr cdivides cprime c2 wcel cz eluzelz ax-mp wa breq1 wi syl wral
      cle wrex cuz cfv wss c0 wne crab ssrab2 eqsstri iddvds elrab2 ne0i sylbir
      mp2an uzwo w3a df-3an simpl a1i simprbi adantl sseli dvdstr mp3an3 syl2an
      wceq mpan2d expcom imp3a jcad syl6ibr 3impib cn c1 eluz2b3 simplbi dvdsle
      cr wb eluzelre nnre letri3 biimprd exp3a syld imim1 syl5 syl5bir ralimdv2
      mpdi com12 isprm4 baib imp adantr jca reximi2 ) AHZGHZUBIZGBUAZABUCZWTDJI
      ZAKUCBLUDUEZUFBUGUHZXDBCHZDJIZCXFUIXFEXICXFUJUKZDXFMZDDJIZXGFDNMZXLXKXMFL
      DOPZDULPXKXLQDBMXGXIXLCDXFBXHDDJREUMBDUNUOUPBAGLUQUPXCXEABKWTBMZXCQWTKMZX
      EXOXCXPXOXCXAWTJIZXAWTVHZSZGXFUAZXPXOXBXSGBXFXABMZXBSZXOXAXFMZXSSYBXOYCXS
      YBXOYCQZXQXRYDXQQXOYCXQURZYBXRXOYCXQUSYBYEYAXRXOYCXQYAXOYCXQQZYCXADJIZQYA
      XOYFYCYGYFYCSXOYCXQUTVAXOYCXQYGYCXOXQYGSYCXOQZXQXEYGXOXEYCXOWTXFMZXEXIXEC
      WTXFBXHWTDJREUMVBZVCYCXANMZWTNMZXQXEQYGSZXOLXAOZXOYIYLBXFWTXJVDZLWTOTYKYL
      XMYMXNXAWTDVEVFVGVIVJVKVLXIYGCXAXFBXHXADJREUMVMVNYEXBXRSZYBYAXRSXOYCXQYPX
      OYCXQYPYCXOXQYPSYHXQXAWTUBIZYPYCYKWTVOMZXQYQSXOYNXOYIYRYOYIYRWTVPUHWTVQVR
      TZXAWTVSVGYHYQXBXRYHXRYQXBQZYCXAVTMWTVTMZXRYTWAXOLXAWBXOYRUUAYSWTWCTXAWTW
      DVGWEWFWGVJVKVNYAXBXRWHWIWLWJWFWFWMWKXOXPXTXOYIXPXTWAYOXPYIXTGWTWNWOTWEWG
      WPXOXEXCYJWQWRWSP $.
      $( [23-Nov-2012] $) $( [26-Oct-2012] $)
  $}

  ${
    $d N m p $.
    $( Every integer greater than or equal to 2 has a prime factor.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    exprmfct $p |- ( N e. ( ZZ>= ` 2 ) -> E. p e. Prime p || N ) $=
      ( vm c2 cuz cfv wcel cdivides wbr cprime wrex cif wceq breq2 rexbidv crab
      cv eqid cz 2z uzid ax-mp elimel exprmfctlem dedth ) ADEFZGZBQZAHIZBJKUHUG
      ADLZHIZBJKADAUJMUIUKBJAUJUHHNOBCQUJHICUFPZCUJULRADUFDSGDUFGTDUAUBUCUDUE
      $.
      $( [26-Oct-2012] $)
  $}

  $( No prime number divides 1.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
  nprmdvds1 $p |- ( P e. Prime -> -. P || 1 ) $=
    ( cprime wcel c1 cdivides wbr clt cz c2 cuz cfv prmuz2 eluz2b1 sylib simprd
    wa cle wn mpan2 cr wi prmz cn 1nn dvdsle syl zre 1re lenlt 3syl sylibd mt2d
    wb ) ABCZADEFZDAGFZUNAHCZUPUNAIJKCUQUPPALAMNOUNUOADQFZUPRZUNUQUOURUAZAUBZUQ
    DUCCUTUDADUESUFUNUQATCZURUSUMZVAAUGVBDTCVCUHADUISUJUKUL $.
    $( [23-Nov-2012] $) $( [17-Nov-2012] $)

  ${
    $d N x y $.  $d N y z $.  $d S x y $.
    maxprmfct.1 $e |- S = { z e. Prime | z || N } $.
    $( The set of prime factors of an integer greater than or equal to 2
       satisfies the conditions to have a supremum, and that supremum is a
       member of the set.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
    maxprmfct $p |- ( N e. ( ZZ>= ` 2 ) ->
         ( ( S C_ RR /\ S =/= (/) /\ E. x e. RR A. y e. S y <_ x ) /\
         sup ( S , RR , < ) e. S ) ) $=
      ( wcel cr wss wne cv cle wbr wral wrex cz cprime cdivides sstri wex c2 c0
      cuz cfv w3a clt csup crab ssrab2 eqsstri prmz ssriv zssre a1i exprmfct wa
      breq1 elrab2 exbii n0 df-rex 3bitr4ri sylib cn c1 eluz2b3 anim1i sylbi wi
      simplbi dvdsle expcom imp3a syl5 ralrimiv nnre wceq breq2 ralbidv rcla4ev
      syl sylan mpdan 3jca suprzcl mp3an1 syl2anc jca ) EUAUCUDGZDHIZDUBJZBKZAK
      ZLMZBDNZAHOZUEDHUFUGDGZWIWJWKWPWJWIDPHDQPDCKZERMZCQUHQFWSCQUIUJBQPWLUKZUL
      SZUMSUNWIWLERMZBQOZWKEBUOWLDGZBTWLQGZXBUPZBTWKXCXDXFBWSXBCWLQDWRWLERUQFUR
      ZUSBDUTXBBQVAVBVCZWIWLELMZBDNZWPWIEVDGZXJWIXKEVEJEVFVJZXKXIBDXDWLPGZXBUPZ
      XKXIXDXFXNXGXEXMXBWTVGVHXKXMXBXIXMXKXBXIVIWLEVKVLVMVNVOWAWIEHGZXJWPWIXKXO
      XLEVPWAWOXJAEHWMEVQWNXIBDWMEWLLVRVSVTWBWCZWDWIWKWPWQXHXPDPIWKWPWQXAABDWEW
      FWGWH $.
      $( [23-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    $d N m $.  $d P k m $.  $d A k m $.
    $( A prime divides a positive power of an integer iff it divides the
       integer.  (Contributed by Mario Carneiro, 24-Feb-2014.) $)
    prmdvdsexp $p |- ( ( P e. Prime /\ A e. ZZ /\ N e. NN ) ->
                       ( P || ( A ^ N ) <-> P || A ) ) $=
      ( vm vk wcel cz cexp co cdivides wi wa c1 wceq oveq2 breq2d imbi1d imbi2d
      wbr syl cprime cn w3a cv caddc cc zcn adantl exp1 biimpd cmul nnnn0 expp1
      wo cn0 syl2an wb simpll simpr zexpcl simplr syl3anc sylbid imimorb sylibr
      euclemma expcom a2d nnind impcom 3impa iddvdsexp 3adant1 simp1 prmz simp2
      simp3 syl2anc dvdstr mpan2d impbid ) BUAFZAGFZCUBFZUCZBACHIZJSZBAJSZWBWCW
      DWGWHKZWDWBWCLZWIWJBADUDZHIZJSZWHKZKWJBAMHIZJSZWHKZKWJBAEUDZHIZJSZWHKZKWJ
      BAWRMUEIZHIZJSZWHKZKWJWIKDECWKMNZWNWQWJXFWMWPWHXFWLWOBJWKMAHOPQRWKWRNZWNX
      AWJXGWMWTWHXGWLWSBJWKWRAHOPQRWKXBNZWNXEWJXHWMXDWHXHWLXCBJWKXBAHOPQRWKCNZW
      NWIWJXIWMWGWHXIWLWFBJWKCAHOPQRWJWPWHWJWOABJWJAUFFZWOANWCXJWBAUGUHZAUITPUJ
      WRUBFZWJXAXEWJXLXAXEKZWJXLLZXDWTWHUNZKXMXNXDBWSAUKIZJSZXOXNXCXPBJWJXJWRUO
      FZXCXPNXLXKWRULZAWRUMUPPXNXQXOXNWBWSGFZWCXQXOUQWBWCXLURWJWCXRXTXLWBWCUSXS
      AWRUTUPWBWCXLVABWSAVFVBUJVCXDWTWHVDVEVGVHVIVJVKWEWHAWFJSZWGWCWDYAWBACVLVM
      WEBGFZWCWFGFZWHYALWGKWEWBYBWBWCWDVNBVOTWBWCWDVPZWEWCCUOFZYCYDWEWDYEWBWCWD
      VQCULTACUTVRBAWFVSVBVTWA $.
      $( [30-Nov-2012] $)
  $}

  $( A prime divides a positive power of another iff they are equal.
     (Contributed by Paul Chapman, 30-Nov-2012.)  (Revised by Mario Carneiro,
     24-Feb-2014.) $)
  prmdvdsexpb $p |- ( ( P e. Prime /\ Q e. Prime /\ N e. NN ) ->
                      ( P || ( Q ^ N ) <-> P = Q ) ) $=
    ( cprime wcel cn w3a cexp co cdivides wceq cz wb prmz prmdvdsexp syl3an2 c2
    wbr cuz cfv prmuz2 dvdsprm sylan 3adant3 bitrd ) ADEZBDEZCFEZGABCHIJRZABJRZ
    ABKZUGUFBLEUHUIUJMBNBACOPUFUGUJUKMZUHUFAQSTEUGULAUABAUBUCUDUE $.
    $( [24-Feb-2014] $) $( [30-Nov-2012] $)

  ${
    $d P z $.
    $( Two positive prime powers are equal iff the primes and the powers are
       equal.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    prmexpb $p |- ( ( ( P e. Prime /\ Q e. Prime ) /\ ( M e. NN /\ N e. NN ) )
                    -> ( ( P ^ M ) = ( Q ^ N ) <-> ( P = Q /\ M = N ) ) ) $=
      ( cprime wcel wa cn cexp co wceq w3a cdivides wbr cz prmz mpbid syl nnz
      wb adantr 3ad2ant1 simp2l iddvdsexp syl2anc 3ad2ant3 simp1l simp1r simp2r
      breq2 prmdvdsexpb syl3anc bitrd simp3 oveq1d eqtr4d cr c1 clt zre cuz cfv
      prmuz2 eluz2b1 simprbi ad2antrr 3adant3 expcan syl31anc jca 3expia oveq12
      c2 impbid1 ) AEFZBEFZGZCHFZDHFZGZGACIJZBDIJZKZABKZCDKZGZVQVTWCWFVQVTWCLZW
      DWEWGAWAMNZWDWGAOFZVRWHVQVTWIWCVOWIVPAPUAUBZVQVRVSWCUCZACUDUEWGWHAWBMNZWD
      WCVQWHWLTVTWAWBAMUJUFWGVOVPVSWLWDTVOVPVTWCUGVOVPVTWCUHVQVRVSWCUIZABDUKULU
      MQZWGWAADIJZKZWEWGWAWBWOVQVTWCUNWGABDIWNUOUPWGAUQFZCOFZDOFZURAUSNZWPWETWG
      WIWQWJAUTRWGVRWRWKCSRWGVSWSWMDSRVQVTWTWCVOWTVPVTVOAVMVAVBFZWTAVCXAWIWTAVD
      VERVFVGACDVHVIQVJVKABCDIVLVN $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d P x k $.  $d N x $.
    $( The factorial of a number only contains primes less than the base.
       (Contributed by Mario Carneiro, 6-Mar-2014.) $)
    prmfac1 $p |- ( ( N e. NN0 /\ P e. Prime /\ P || ( ! ` N ) ) -> P <_ N ) $=
      ( wcel cfa cfv cdivides wbr cle wi cc0 c1 wceq fveq2 breq2d breq2 imbi12d
      imbi2d adantr cz syl vx vk cn0 cprime caddc fac0 breq2i nprmdvds1 pm2.21d
      cv co syl5bi wa wo cmul facp1 wb simpr faccl nnz nn0p1nn euclemma syl3anc
      cn bitrd nn0re lep1 prmz adantl zre nnre letr mpan2d imim2d com23 syl2anc
      cr dvdsle a1dd jaod sylbid ex a2d nn0ind 3imp ) BUCCAUDCZABDEZFGZABHGZWFA
      UAUJZDEZFGZAWJHGZIZIWFAJDEZFGZAJHGZIZIWFAUBUJZDEZFGZAWSHGZIZIWFAWSKUEUKZD
      EZFGZAXDHGZIZIWFWHWIIZIUAUBBWJJLZWNWRWFXJWLWPWMWQXJWKWOAFWJJDMNWJJAHOPQWJ
      WSLZWNXCWFXKWLXAWMXBXKWKWTAFWJWSDMNWJWSAHOPQWJXDLZWNXHWFXLWLXFWMXGXLWKXEA
      FWJXDDMNWJXDAHOPQWJBLZWNXIWFXMWLWHWMWIXMWKWGAFWJBDMNWJBAHOPQWPAKFGZWFWQWO
      KAFUFUGWFXNWQAUHUIULWSUCCZWFXCXHXOWFXCXHIXOWFUMZXFXCXGXPXFXAAXDFGZUNZXCXG
      IZXPXFAWTXDUOUKZFGZXRXPXEXTAFXOXEXTLWFWSUPRNXPWFWTSCZXDSCZYAXRUQXOWFURXPW
      TVDCZYBXOYDWFWSUSRWTUTTXPXDVDCZYCXOYEWFWSVARZXDUTTAWTXDVBVCVEXPXAXSXQXPXC
      XAXGXPXBXGXAXPXBWSXDHGZXGXPWSVQCZYGXOYHWFWSVFRZWSVGTXPAVQCZYHXDVQCZXBYGUM
      XGIXPASCZYJWFYLXOAVHVIZAVJTYIXPYEYKYFXDVKTAWSXDVLVCVMVNVOXPXQXGXCXPYLYEXQ
      XGIYMYFAXDVRVPVSVTWAVOWBWCWDWE $.
      $( [6-Mar-2014] $)
  $}

