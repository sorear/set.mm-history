$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             The divides relation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c || $.

  $( Extend the definition of a class to include the divides relation. $)
  cdivides $a class || $.

  ${
    $d n x y $.
    $( Define the divides relation. $)
    df-divides $a |- || = { <. x , y >. | ( ( x e. ZZ /\ y e. ZZ ) /\
                                            E. n e. ZZ ( n x. x ) = y ) } $.
  $}

  ${
    $d M n x y $.  $d N n x y $.
    $( Divides relation. ` M || N ` means ` M ` divides into ` N ` with no
       remainder.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    divides $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                    ( M || N <-> E. n e. ZZ ( n x. M ) = N ) ) $=
      ( vx vy cdivides wbr cop cv cz wcel wa cmul co wceq wrex copab df-divides
      df-br rexbidv eleq2i bitri oveq2 eqeq1d eqeq2 opelopab2 syl5bb ) BCFGZBCH
      ZDIZJKEIZJKLAIZUJMNZUKOZAJPZLDEQZKZBJKCJKLULBMNZCOZAJPZUHUIFKUQBCFSFUPUID
      EARUAUBUOURUKOZAJPUTDEBCJJUJBOZUNVAAJVBUMURUKUJBULMUCUDTUKCOVAUSAJUKCURUE
      TUFUG $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d k M $.  $d k N $.
    $( One nonzero integer divides another integer if and only if their
       quotient is an integer.  (Contributed by Jeff Hankins, 29-Sep-2013.) $)
    divides2 $p |- ( ( M e. ZZ /\ M =/= 0 /\ N e. ZZ ) -> ( M || N <-> ( N / M
    ) e. ZZ ) ) $=
      ( vk cz wcel cc0 wne w3a cdivides wbr cv cmul co wceq wb wa cc zcn adantr
      wrex cdiv divides 3adant2 3ad2ant3 adantl 3ad2ant1 simpl2 syl112anc eqcom
      divmul3 syl6bb biimprd impr simprl eqeltrd exp32 rexlimdv divcan1 syl3anc
      simpr simp2 oveq1 eqeq1d rcla4ev syl2anc ex impbid bitrd ) ADEZAFGZBDEZHZ
      ABIJZCKZALMZBNZCDTZBAUAMZDEZVIVKVMVQOVJCABUBUCVLVQVSVLVPVSCDVLVNDEZVPVSVL
      VTVPPPVRVNDVLVTVPVRVNNZVLVTPZWAVPWBWABVONZVPWBBQEZVNQEZAQEZVJWAWCOVLWDVTV
      KVIWDVJBRUDZSVTWEVLVNRUEVLWFVTVIVJWFVKARUFZSVIVJVKVTUGBVNAUJUHBVOUIUKULUM
      VLVTVPUNUOUPUQVLVSVQVLVSPVSVRALMZBNZVQVLVSUTVLWJVSVLWDWFVJWJWGWHVIVJVKVAB
      AURUSSVPWJCVRDVNVRNVOWIBVNVRALVBVCVDVEVFVGVH $.
      $( [29-Sep-2013] $)
  $}

  ${
    $d k M $.  $d k N $.
    $( One nonzero integer divides another integer if and only if the remainder
       upon division is zero.  (Contributed by Mario Carneiro, 22-Feb-2014.) $)
    divides3 $p |- ( ( M e. NN /\ N e. ZZ ) ->
                     ( M || N <-> ( N mod M ) = 0 ) ) $=
      ( vk wcel cz wa wbr cmul co wceq cmo cc0 cr 0re cc zcn syl2anr syl adantr
      syl2anc cn cdivides cv wrex wb nnz divides sylan wi caddc crp nnrp modcyc
      mp3an1 nncn mulcl addid2 oveq1d cle jctil nngt0 leidi modid 3eqtr3d oveq1
      clt eqeq1d syl5ibcom rexlimdva cdiv cfl cfv cmin adantl zre flcl ax-mulcl
      id nndivre subeq0 modval eqcom mulcom eqeq2d syl5bb 3bitr4d biimpd jctild
      rcla4ev syl6 impbid bitrd ) AUADZBEDZFZABUBGZCUCZAHIZBJZCEUDZBAKIZLJZWMAE
      DWNWPWTUEAUFCABUGUHWOWTXBWMWTXBUIWNWMWSXBCEWMWQEDZFZWRAKIZLJWSXBXDLWRUJIZ
      AKIZLAKIZXELWMAUKDZXCXGXHJZAULZLMDZXIXCXJNLAWQUMUNUHXDXFWRAKXDWRODZXFWRJX
      CWQODAODZXMWMWQPAUOZWQAUPQWRUQRURWMXHLJZXCWMXLXIFLLUSGZLAVFGZFXPWMXIXLXKN
      UTWMXRXQAVALNVBUTLAVCTSVDWSXEXALWRBAKVEVGVHVISWOXBBAVJIZVKVLZEDZXTAHIZBJZ
      FWTWOXBYCYAWOXBYCWOBAXTHIZVMIZLJZBYDJZXBYCWOBODZYDODZYFYGUEWNYHWMBPVNWOXN
      XTODZYIWMXNWNXOSZWOYAYJWOXSMDZYAWNBMDZWMYLWMBVOZWMVRBAVSQXSVPRZXTPRZAXTVQ
      TBYDVTTWOXAYELWNYMXIXAYEJWMYNXKBAWAQVGYCBYBJWOYGYBBWBWOYBYDBWOYJXNYBYDJYP
      YKXTAWCTWDWEWFWGYOWHWSYCCXTEWQXTJWRYBBWQXTAHVEVGWIWJWKWL $.
      $( [22-Feb-2014] $)
  $}

  ${
    $d K x $.  $d M x $.  $d N x $.
    $( A lemma to assist theorems of ` || ` with no antecedents.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    dvds0lem $p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\ ( K x. M ) = N ) ->
                     M || N ) $=
      ( vx cz wcel w3a cmul co wceq cdivides wi wa cv wrex oveq1 eqeq1d rcla4ev
      wbr adantl wb divides adantr mpbird 3impb 3expia 3impa 3comr imp ) AEFZBE
      FZCEFZGABHIZCJZBCKSZUKULUJUNUOLZUKULUJUPUKULMZUJUNUOUQUJUNUOUQUJUNMZMUODN
      ZBHIZCJZDEOZURVBUQVAUNDAEUSAJUTUMCUSABHPQRTUQUOVBUAURDBCUBUCUDUEUFUGUHUI
      $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d J x $.  $d K x $.  $d M x z $.  $d N x z $.  $d Z z $.  $d ph x $.
    dvds1lem.1 $e |- ( ph -> ( J e. ZZ /\ K e. ZZ ) ) $.
    dvds1lem.2 $e |- ( ph -> ( M e. ZZ /\ N e. ZZ ) ) $.
    dvds1lem.3 $e |- ( ( ph /\ x e. ZZ ) -> Z e. ZZ ) $.
    dvds1lem.4 $e |- ( ( ph /\ x e. ZZ ) ->
                       ( ( x x. J ) = K -> ( Z x. M ) = N ) ) $.
    $( A lemma to assist theorems of ` || ` with one antecedent.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    dvds1lem $p |- ( ph -> ( J || K -> M || N ) ) $=
      ( vz cdivides wbr cmul co wceq cz wcel wa cv wrex wb divides biimpd oveq1
      syl wi eqeq1d rcla4ev sylan ex syld rexlimdv sylibrd ) ACDMNZLUAZEOPZFQZL
      RUBZEFMNZAUPBUAZCOPDQZBRUBZUTAUPVDACRSDRSTUPVDUCHBCDUDUGUEAVCUTBRAVBRSZVC
      UTUHAVETZVCGEOPZFQZUTKVFVHUTVFGRSVHUTJUSVHLGRUQGQURVGFUQGEOUFUIUJUKULUMUL
      UNUMAERSFRSTVAUTUCILEFUDUGUO $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d I x y $.  $d J x y $.  $d K x y $.  $d L x y $.  $d M x y z $.
    $d N x y z $.  $d Z z $.  $d ph x y $.
    dvds2lem.1 $e |- ( ph -> ( I e. ZZ /\ J e. ZZ ) ) $.
    dvds2lem.2 $e |- ( ph -> ( K e. ZZ /\ L e. ZZ ) ) $.
    dvds2lem.3 $e |- ( ph -> ( M e. ZZ /\ N e. ZZ ) ) $.
    dvds2lem.4 $e |- ( ( ph /\ ( x e. ZZ /\ y e. ZZ ) ) -> Z e. ZZ ) $.
    dvds2lem.5 $e |- ( ( ph /\ ( x e. ZZ /\ y e. ZZ ) ) ->
               ( ( ( x x. I ) = J /\ ( y x. K ) = L ) -> ( Z x. M ) = N ) ) $.
    $( A lemma to assist theorems of ` || ` with two antecedents.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    dvds2lem $p |- ( ph -> ( ( I || J /\ K || L ) -> M || N ) ) $=
      ( wa cmul wceq cz wcel vz cdivides cv co wrex wb divides bi2anan9 syl2anc
      wbr biimpd reeanv syl6ibr wi oveq1 eqeq1d rcla4ev sylan ex syld rexlimdvv
      syl sylibrd ) ADEUBUJZFGUBUJZPZUAUCZHQUDZIRZUASUEZHIUBUJZAVFBUCZDQUDERZCU
      CZFQUDGRZPZCSUEBSUEZVJAVFVMBSUEZVOCSUEZPZVQAVFVTADSTESTPZFSTGSTPZVFVTUFKL
      WAVDVRWBVEVSBDEUGCFGUGUHUIUKVMVOBCSSULUMAVPVJBCSSAVLSTVNSTPZVPVJUNAWCPZVP
      JHQUDZIRZVJOWDWFVJWDJSTWFVJNVIWFUAJSVGJRVHWEIVGJHQUOUPUQURUSUTUSVAUTAHSTI
      STPVKVJUFMUAHIUGVBVC $.
      $( [21-Mar-2011] $)
  $}

  $( An integer divides itself.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  iddvds $p |- ( N e. ZZ -> N || N ) $=
    ( cz wcel c1 cmul co wceq cdivides wbr cc zcn mulid2 syl 1z mp3anl1 anabsan
    dvds0lem mpdan ) ABCZDAEFAGZAAHIZSAJCTAKALMSTUADBCSSTUANDAAQOPR $.
    $( [21-Mar-2011] $)

  $( 1 divides any integer.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  1dvds $p |- ( N e. ZZ -> 1 || N ) $=
    ( cz wcel c1 cmul co wceq cdivides wbr cc zcn mulid1 syl 1z mp3anl2 anabsan
    dvds0lem mpdan ) ABCZADEFAGZDAHIZSAJCTAKALMSTUASDBCSTUANADAQOPR $.
    $( [21-Mar-2011] $)

  $( Any integer divides 0.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  dvds0 $p |- ( N e. ZZ -> N || 0 ) $=
    ( cz wcel cc0 cmul co wceq cdivides wbr cc zcn mul02 syl wi 0z w3a dvds0lem
    ex mp3an13 mpd ) ABCZDAEFDGZADHIZUAAJCUBAKALMDBCZUAUDUBUCNOOUDUAUDPUBUCDADQ
    RST $.
    $( [21-Mar-2011] $)

  ${
    $d M x $.  $d N x $.
    $( An integer divides another iff its negation does.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    negdvdsb $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> -u M || N ) ) $=
      ( vx cz wcel wa cdivides wbr cneg znegcl cmul co cc syl2an ancoms adantlr
      wceq zcn eqeq1d biimprd cv anim1i adantl mul2neg dvds1lem mulneg12 impbid
      id ) ADEZBDEZFZABGHAIZBGHUKCABULBCUAZIZUKUHZUIULDEUJAJUBZUMDEZUNDEUKUMJUC
      ZUKUQFZUNULKLZBQUMAKLZBQUSUTVABUIUQUTVAQZUJUQUIVBUQUMMEZAMEZVBUIUMRZARZUM
      AUDNOPSTUEUKCULBABUNUPUOURUSUNAKLZBQUMULKLZBQUSVGVHBUIUQVGVHQZUJUQUIVIUQV
      CVDVIUIVEVFUMAUFNOPSTUEUG $.
      $( [21-Mar-2011] $)

    $( An integer divides another iff it divides its negation.  (Contributed by
       Paul Chapman, 21-Mar-2011.) $)
    dvdsnegb $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || -u N ) ) $=
      ( vx cz wcel wa cdivides wbr cneg cv id znegcl cmul co wi cc zcn dvds1lem
      wceq negeq anim2i adantl mulneg1 eqeq2d syl5ibcom syl2an ancoms sylan9eqr
      adantlr negneg sylan9eq anassrs ex 3impa syl3an 3coml 3expa impbid ) ADEZ
      BDEZFZABGHABIZGHVACABAVBCJZIZVAKZUTVBDEUSBLUAZVCDEZVDDEVAVCLUBZUSVGVCAMNZ
      BSZVDAMNZVBSZOZUTVGUSVMVGVCPEZAPEZVMUSVCQZAQZVNVOFZVKVIIZSVJVLVCAUCZVJVSV
      BVKVIBTUDUEUFUGUIRVACAVBABVDVFVEVHUSUTVGVIVBSZVKBSZOZVGUSUTWCVGVNUSVOUTBP
      EZWCVPVQBQVNVOWDWCVRWDFWAWBVRWDWAWBVRWDWAFVKVSBVTWAWDVSVBIBVIVBTBUJUHUKUL
      UMUNUOUPUQRUR $.
      $( [21-Mar-2011] $)
  $}

  $( An integer divides another iff its absolute value does.  (Contributed by
     Paul Chapman, 21-Mar-2011.) $)
  absdvdsb $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                   ( M || N <-> ( abs ` M ) || N ) ) $=
    ( cz wcel wa cabs cfv wceq wo cdivides wbr wb cr zre absor syl adantr breq1
    cneg bicomd wi a1i negdvdsb anim12i bitr ex jaod mpd ) ACDZBCDZEZAFGZAHZULA
    SZHZIZABJKZULBJKZLZUIUPUJUIAMDUPANAOPQUKUMUSUOUMUSUAUKUMURUQULABJRTUBUKUOUS
    UKUOEUQUNBJKZLZUTURLZEUSUKVAUOVBABUCUOURUTULUNBJRTUDUQUTURUEPUFUGUH $.
    $( [21-Mar-2011] $)

  $( An integer divides another iff it divides its absolute value.
     (Contributed by Paul Chapman, 21-Mar-2011.) $)
  dvdsabsb $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                   ( M || N <-> M || ( abs ` N ) ) ) $=
    ( cz wcel wa cabs cfv wceq wo cdivides wbr wb cr zre absor syl adantl breq2
    cneg bicomd wi a1i dvdsnegb anim12i bitr ex jaod mpd ) ACDZBCDZEZBFGZBHZULB
    SZHZIZABJKZAULJKZLZUJUPUIUJBMDUPBNBOPQUKUMUSUOUMUSUAUKUMURUQULBAJRTUBUKUOUS
    UKUOEUQAUNJKZLZUTURLZEUSUKVAUOVBABUCUOURUTULUNAJRTUDUQUTURUEPUFUGUH $.
    $( [21-Mar-2011] $)

  ${
    $d N n $.
    $( Only 0 is divisible by 0 .  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    0dvds $p |- ( N e. ZZ -> ( 0 || N <-> N = 0 ) ) $=
      ( vn cz wcel cc0 cdivides wbr wceq cv cmul co wrex wb 0z divides mpan zcn
      cc mul01 syl eqtr2 sylan2 ancoms syl6bi dvds0 ax-mp breq2 mpbiri impbid1
      rexlimiva ) ACDZEAFGZAEHZUKULBIZEJKZAHZBCLZUMECDZUKULUQMNBEAOPUPUMBCUPUNC
      DZUMUSUPUOEHZUMUSUNRDUTUNQUNSTUOAEUAUBUCUJUDUMULEEFGZURVANEUEUFAEEFUGUHUI
      $.
      $( [21-Mar-2011] $)
  $}

  $( An integer divides a multiple of itself.  (Contributed by Paul Chapman,
     21-Mar-2011.) $)
  dvdsmul1 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> M || ( M x. N ) ) $=
    ( cz wcel wa cmul co cdivides wbr cc zcn mulcom syl2an ancoms wi zmulcl w3a
    wceq dvds0lem ex 3com12 mpd3an3 mpd ) ACDZBCDZEBAFGABFGZRZAUFHIZUEUDUGUEBJD
    AJDUGUDBKAKBALMNUDUEUFCDZUGUHOZABPUEUDUIUJUEUDUIQUGUHBAUFSTUAUBUC $.
    $( [21-Mar-2011] $)

  $( An integer divides a multiple of itself.  (Contributed by Paul Chapman,
     21-Mar-2011.) $)
  dvdsmul2 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> N || ( M x. N ) ) $=
    ( cz wcel cmul co cdivides wbr zmulcl w3a wceq eqid dvds0lem mpan2 mpd3an3
    ) ACDZBCDZABEFZCDZBRGHZABIPQSJRRKTRLABRMNO $.
    $( [21-Mar-2011] $)

  $( An integer divides a positive integer power of itself.  (Contributed by
     Paul Chapman, 26-Oct-2012.) $)
  iddvdsexp $p |- ( ( M e. ZZ /\ N e. NN ) -> M || ( M ^ N ) ) $=
    ( cz wcel cn wa cexp co cdivides wbr c1 cmin cmul cn0 nnm1nn0 zexpcl sylan2
    simpl dvdsmul2 syl2anc cc wceq zcn expm1t sylan breq2d mpbird ) ACDZBEDZFZA
    ABGHZIJAABKLHZGHZAMHZIJZUJUMCDZUHUOUIUHULNDUPBOAULPQUHUIRUMASTUJUKUNAIUHAUA
    DUIUKUNUBAUCABUDUEUFUG $.
    $( [26-Oct-2012] $)

  ${
    $d K x $.  $d M x $.  $d N x $.
    $( If a product divides an integer, so does one of its factors.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    muldvds1 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K x. M ) || N -> K || N ) ) $=
      ( vx cz wcel w3a cmul co cv wa zmulcl id anim12i 3impa 3simpb ancoms wceq
      cc zcn 3ad2antl2 mulass mul32 eqtr3d syl3an 3coml 3expa 3adantl3 dvds1lem
      eqeq1d biimpd ) AEFZBEFZCEFZGZDABHIZCACDJZBHIZULUMUNUPEFZUNKULUMKUSUNUNAB
      LUNMNOULUMUNPUMULUQEFZUREFZUNUTUMVAUQBLQUAUOUTKZUQUPHIZCRURAHIZCRVBVCVDCU
      LUMUTVCVDRZUNULUMUTVEUTULUMVEUTUQSFZULASFZUMBSFZVEUQTATBTVFVGVHGUQAHIBHIV
      CVDUQABUBUQABUCUDUEUFUGUHUJUKUI $.
      $( [21-Mar-2011] $)

    $( If a product divides an integer, so does one of its factors.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    muldvds2 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K x. M ) || N -> M || N ) ) $=
      ( vx cz wcel w3a cmul co cv wa zmulcl id anim12i 3impa 3simpc ancoms wceq
      cc zcn 3ad2antl1 mulass syl3an 3coml 3adantl3 eqeq1d biimprd dvds1lem
      3expa ) AEFZBEFZCEFZGZDABHIZCBCDJZAHIZUJUKULUNEFZULKUJUKKUQULULABLULMNOUJ
      UKULPUJUKUOEFZUPEFZULURUJUSUOALQUAUMURKZUPBHIZCRUOUNHIZCRUTVAVBCUJUKURVAV
      BRZULUJUKURVCURUJUKVCURUOSFUJASFUKBSFVCUOTATBTUOABUBUCUDUIUEUFUGUH $.
      $( [21-Mar-2011] $)

    $( Multiplication by a constant maintains the divides relation.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdscmul $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) ->
                     ( M || N -> ( K x. M ) || ( K x. N ) ) ) $=
      ( vx cz wcel cdivides wbr cmul co wi w3a cv 3simpc zmulcl 3adant3 wceq cc
      zcn 3coml 3adant2 jca simpr wa mul12 syl3an 3expa 3adantl3 oveq2 sylan9eq
      ex dvds1lem ) AEFZBEFZCEFZBCGHABIJZACIJZGHKUMUNUOLZDBCUPUQDMZUMUNUONURUPE
      FZUQEFZUMUNUTUOABOPUMUOVAUNACOUAUBURUSEFZUCURVBUDZUSBIJZCQZUSUPIJZUQQVCVE
      VFAVDIJZUQUMUNVBVFVGQZUOUMUNVBVHVBUMUNVHVBUSRFUMARFUNBRFVHUSSASBSUSABUEUF
      TUGUHVDCAIUIUJUKULT $.
      $( [21-Mar-2011] $)

    $( Multiplication by a constant maintains the divides relation.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdsmulc $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) ->
                     ( M || N -> ( M x. K ) || ( N x. K ) ) ) $=
      ( vx cz wcel cdivides wbr cmul co wi w3a cv 3simpc wa zmulcl 3adant2 wceq
      cc zcn 3adant1 jca 3comr simpr mulass syl3an 3com13 3expa oveq1 sylan9req
      3adantl3 ex dvds1lem 3coml ) AEFZBEFZCEFZBCGHBAIJZCAIJZGHKUOUPUQLZDBCURUS
      DMZUOUPUQNUPUQUOUREFZUSEFZOUPUQUOLVBVCUPUOVBUQBAPQUQUOVCUPCAPUAUBUCUTVAEF
      ZUDUTVDOZVABIJZCRZVAURIJZUSRVEVGVHVFAIJZUSUOUPVDVIVHRZUQUOUPVDVJVDUPUOVJV
      DVASFUPBSFUOASFVJVATBTATVABAUEUFUGUHUKVFCAIUIUJULUMUN $.
      $( [21-Mar-2011] $)

    $( Cancellation law for the divides relation.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    dvdscmulr $p |- ( ( M e. ZZ /\ N e. ZZ /\ ( K e. ZZ /\ K =/= 0 ) ) ->
                      ( ( K x. M ) || ( K x. N ) <-> M || N ) ) $=
      ( vx cz wcel wa w3a cmul co cdivides wbr zmulcl 3adant2 3coml 3adant3r wb
      wceq cc zcn cc0 wne cv 3adant3 jca 3simpa simpr wi anim12i mul12 3adant1r
      anim1i 3expb ancoms eqeq1d mulcl mulcan syl3an1 bitr3d syl3an 3impa 3impb
      3expia imp biimpd dvds1lem dvdscmul impbid ) BEFZCEFZAEFZAUAUBZGZHZABIJZA
      CIJZKLZBCKLZVNDVOVPBCDUCZVIVJVKVOEFZVPEFZGZVLVKVIVJWBVKVIVJHVTWAVKVIVTVJA
      BMUDVKVJWAVIACMNUEOPVIVJVMUFVNVSEFZUGVNWCGVSVOIJZVPRZVSBIJZCRZVNWCWEWGQZV
      IVJVMWCWHUHVIVJVMGZWCWHWCVIWIWHWCVIWIWHWCVIGZVJVMWHWJVSSFZBSFZGZVJCSFZVMA
      SFZVLGZWHWCWKVIWLVSTBTUICTVKWOVLATULWMWNWPHZAWFIJZVPRZWEWGWQWRWDVPWMWPWRW
      DRZWNWPWMWTWPWKWLWTWOWKWLWTVLAVSBUJUKUMUNNUOWMWFSFWNWPWSWGQVSBUPWFCAUQURU
      SUTUMVAOVCVBVDVEVFVIVJVKVRVQUHVLABCVGPVH $.
      $( [21-Mar-2011] $)

    $( Cancellation law for the divides relation.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    dvdsmulcr $p |- ( ( M e. ZZ /\ N e. ZZ /\ ( K e. ZZ /\ K =/= 0 ) ) ->
                      ( ( M x. K ) || ( N x. K ) <-> M || N ) ) $=
      ( vx cz wcel wa w3a cmul co cdivides wbr zmulcl 3adant2 3adant3r wb wi cc
      wceq zcn cc0 wne cv 3adant1 jca 3simpa simpr anim12i anim1i 3expa adantrr
      mulass eqeq1d mulcl mulcan2 syl3an1 bitr3d syl3an 3expb 3impa 3coml 3impb
      3expia imp biimpd dvds1lem dvdsmulc impbid ) BEFZCEFZAEFZAUAUBZGZHZBAIJZC
      AIJZKLZBCKLZVNDVOVPBCDUCZVIVJVKVOEFZVPEFZGVLVIVJVKHVTWAVIVKVTVJBAMNVJVKWA
      VICAMUDUEOVIVJVMUFVNVSEFZUGVNWBGVSVOIJZVPSZVSBIJZCSZVNWBWDWFPZVIVJVMWBWGQ
      VIVJVMGZWBWGWBVIWHWGWBVIWHWGWBVIGZVJVMWGWIVSRFZBRFZGZVJCRFZVMARFZVLGZWGWB
      WJVIWKVSTBTUHCTVKWNVLATUIWLWMWOHZWEAIJZVPSZWDWFWPWQWCVPWLWOWQWCSZWMWLWNWS
      VLWJWKWNWSVSBAULUJUKNUMWLWERFWMWOWRWFPVSBUNWECAUOUPUQURUSUTVAVCVBVDVEVFVI
      VJVKVRVQQVLABCVGOVH $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d I x y $.  $d J x y $.  $d K x y $.  $d M x y $.  $d N x y $.
    $( If an integer divides each of two other integers, it divides any linear
       combination of them.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvds2ln $p |- ( ( ( I e. ZZ /\ J e. ZZ ) /\
                      ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ) ->
        ( ( K || M /\ K || N ) -> K || ( ( I x. M ) + ( J x. N ) ) ) ) $=
      ( cz wcel wa cmul co caddc jca zmulcl syl2anc syl wceq zcn adantr adantl
      cc vx vy cv simpr simp1d simp2d simp3d simpl simpld simprd zaddcl anim12i
      w3a wi an4s expcom imp adddir 3expa ad3antrrr mul32 syl3anc oveq12d mulcl
      mulcom 3eqtrd oveq2 oveqan12d sylan9eq ex dvds2lem ) AFGZBFGZHZCFGZDFGZEF
      GZUMZHZUAUBCDCECADIJZBEIJZKJZUAUCZAIJZUBUCZBIJZKJZVSVOVPVSVOVPVQVNVRUDZUE
      ZVSVOVPVQWHUFZLVSVOVQWIVSVOVPVQWHUGZLVSVOWBFGZWIVSVTFGZWAFGZWLVSVLVPWMVSV
      LVMVNVRUHZUIZWJADMNVSVMVQWNVSVLVMWOUJZWKBEMNVTWAUKNLVSWCFGZWEFGZHZHZWDFGZ
      WFFGZHZWGFGVSWTXDVSVLVMWTXDUNWPWQWTVNXDWRVLWSVMXDWRVLHXBWSVMHXCWCAMWEBMUL
      UOUPNUQZWDWFUKOXAWCCIJZDPZWECIJZEPZHZWGCIJZWBPXAXJXKAXFIJZBXHIJZKJZWBXAXK
      WDCIJZWFCIJZKJZXFAIJZXHBIJZKJXNXAWDTGZWFTGZHZCTGZXKXQPZXAXDYBXEXBXTXCYAWD
      QWFQULOVSYCWTVSVOYCWICQORZXTYAYCYDWDWFCURUSNXAXOXRXPXSKXAWCTGZATGZYCXOXRP
      WTYFVSWRYFWSWCQRSZVLYGVMVRWTAQUTZYEWCACVAVBXAWETGZBTGZYCXPXSPWTYJVSWSYJWR
      WEQSSZVSYKWTVSVMYKWQBQORZYEWEBCVAVBVCXAXRXLXSXMKXAXFTGZYGXRXLPXAYFYCYNYHY
      EWCCVDNYIXFAVENXAXHTGZYKXSXMPXAYJYCYOYLYEWECVDNYMXHBVENVCVFXGXIXLVTXMWAKX
      FDAIVGXHEBIVGVHVIVJVK $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K x y $.  $d M x y $.  $d N x y $.
    $( If an integer divides each of two other integers, it divides their sum.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvds2add $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K || M /\ K || N ) -> K || ( M + N ) ) ) $=
      ( vx vy cz wcel w3a caddc co cv 3simpa 3simpb wa id zaddcl cmul wceq zcn
      cc 3impb adantl wi adddir syl3an 3comr 3expb oveq12 sylan9eq ex 3ad2antl1
      anim12i dvds2lem ) AFGZBFGZCFGZHZDEABACABCIJZDKZEKZIJZUNUOUPLUNUOUPMUNUOU
      PUNURFGZNUNUNUOUPNVBUNOBCPULUAUSFGZUTFGZNZVAFGUQUSUTPUBUNUOVEUSAQJZBRUTAQ
      JZCRNZVAAQJZURRZUCUPUNVENZVHVJVKVHVIVFVGIJZURUNVCVDVIVLRZVCVDUNVMVCUSTGVD
      UTTGUNATGVMUSSUTSASUSUTAUDUEUFUGVFBVGCIUHUIUJUKUM $.
      $( [21-Mar-2011] $)

    $( If an integer divides each of two other integers, it divides their
       difference.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvds2sub $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K || M /\ K || N ) -> K || ( M - N ) ) ) $=
      ( vx vy cz wcel w3a cmin co cv 3simpa 3simpb wa id zsubcl cmul wceq zcn
      cc 3impb adantl wi subdir syl3an 3comr 3expb oveq12 sylan9eq ex 3ad2antl1
      anim12i dvds2lem ) AFGZBFGZCFGZHZDEABACABCIJZDKZEKZIJZUNUOUPLUNUOUPMUNUOU
      PUNURFGZNUNUNUOUPNVBUNOBCPULUAUSFGZUTFGZNZVAFGUQUSUTPUBUNUOVEUSAQJZBRUTAQ
      JZCRNZVAAQJZURRZUCUPUNVENZVHVJVKVHVIVFVGIJZURUNVCVDVIVLRZVCVDUNVMVCUSTGVD
      UTTGUNATGVMUSSUTSASUSUTAUDUEUFUGVFBVGCIUHUIUJUKUM $.
      $( [21-Mar-2011] $)

    $( The divides relation is transitive.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    dvdstr $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                   ( ( K || M /\ M || N ) -> K || N ) ) $=
      ( vx vy cz wcel w3a cv cmul co 3simpa 3simpc 3simpb wa zmulcl adantl wceq
      cc zcn oveq2 adantr eqeq2 mpbid mulass mul12 eqtrd syl3an 3comr 3ad2antl1
      wb 3expb eqeq1d syl5ibr dvds2lem ) AFGZBFGZCFGZHZDEABBCACDIZEIZJKZUPUQURL
      UPUQURMUPUQURNUTFGZVAFGZOZVBFGUSUTVAPQUTAJKZBRZVABJKZCRZOZVBAJKZCRUSVEOZV
      AVFJKZCRZVJVMVHRZVNVGVOVIVFBVAJUAUBVIVOVNUKVGVHCVMUCQUDVLVKVMCUPUQVEVKVMR
      ZURUPVCVDVPVCVDUPVPVCUTSGZVDVASGZUPASGZVPUTTVATATVQVRVSHVKUTVAAJKJKVMUTVA
      AUEUTVAAUFUGUHUIULUJUMUNUO $.
      $( [21-Mar-2011] $)
  $}

  $( If an integer divides another, it divides a multiple of it.  (Contributed
     by Paul Chapman, 17-Nov-2012.) $)
  dvdsmultr1 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( K || M -> K || ( M x. N ) ) ) $=
    ( cz wcel w3a cdivides wbr cmul co wa dvdsmul1 3adant1 biantrud simp1 simp2
    wi zmulcl dvdstr syl3anc sylbid ) ADEZBDEZCDEZFZABGHZUFBBCIJZGHZKZAUGGHZUEU
    HUFUCUDUHUBBCLMNUEUBUCUGDEZUIUJQUBUCUDOUBUCUDPUCUDUKUBBCRMABUGSTUA $.
    $( [17-Nov-2012] $)

  $( If an integer divides another, it divides a multiple of it.  (Contributed
     by Paul Chapman, 17-Nov-2012.) $)
  dvdsmultr2 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( K || N -> K || ( M x. N ) ) ) $=
    ( cz wcel w3a cdivides wbr cmul co wa wb dvdsmul2 biantrud 3adant1 wi simp1
    simp3 zmulcl dvdstr syl3anc sylbid ) ADEZBDEZCDEZFZACGHZUGCBCIJZGHZKZAUHGHZ
    UDUEUGUJLUCUDUEKUIUGBCMNOUFUCUEUHDEZUJUKPUCUDUEQUCUDUERUDUEULUCBCSOACUHTUAU
    B $.
    $( [17-Nov-2012] $)

  $( If an integer divides either of two others, it divides their product.
     (Contributed by Paul Chapman, 17-Nov-2012.) $)
  ordvdsmul $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                    ( ( K || M \/ K || N ) -> K || ( M x. N ) ) ) $=
    ( cz wcel w3a cdivides wbr cmul co wa wb dvdsmul1 biantrud 3adant1 wi simp1
    dvdstr syl3anc sylbid simp2 zmulcl dvdsmul2 simp3 jaod ) ADEZBDEZCDEZFZABGH
    ZABCIJZGHZACGHZUIUJUJBUKGHZKZULUGUHUJUOLUFUGUHKZUNUJBCMNOUIUFUGUKDEZUOULPUF
    UGUHQZUFUGUHUAUGUHUQUFBCUBOZABUKRSTUIUMUMCUKGHZKZULUGUHUMVALUFUPUTUMBCUCNOU
    IUFUHUQVAULPURUFUGUHUDUSACUKRSTUE $.
    $( [17-Nov-2012] $)

  $( An integer divides another iff it divides their sum.  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  dvdsadd $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( M + N ) ) ) $=
    ( cz wcel wa cdivides wbr caddc co wi iddvds adantr dvds2add 3anidm12 mpdan
    expdimp cmin simpl cc zcn zaddcl dvds2sub syl3anc wceq pncan2 syl2an breq2d
    ancomsd sylibd impbid ) ACDZBCDZEZABFGZAABHIZFGZUMAAFGZUNUPJUKUQULAKLZUMUQU
    NUPUKULUQUNEUPJAABMNPOUMUPAUOAQIZFGZUNUMUQUPUTJURUMUQUPUTUMUPUQUTUMUKUOCDUK
    UPUQEUTJUKULRZABUAVAAUOAUBUCUHPOUMUSBAFUKASDBSDUSBUDULATBTABUEUFUGUIUJ $.
    $( [21-Mar-2011] $)

  $( An integer divides another iff it divides their sum.  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  dvdsaddr $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( N + M ) ) ) $=
    ( cz wcel wa cdivides wbr caddc co dvdsadd cc wceq zcn addcom syl2an breq2d
    bitrd ) ACDZBCDZEZABFGAABHIZFGABAHIZFGABJTUAUBAFRAKDBKDUAUBLSAMBMABNOPQ $.
    $( [31-Mar-2011] $)

  $( An integer divides another iff it divides their difference.  (Contributed
     by Paul Chapman, 31-Mar-2011.) $)
  dvdssub $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( M - N ) ) ) $=
    ( cz wcel wa cdivides wbr cneg caddc co cmin dvdsnegb znegcl dvdsadd sylan2
    wb cc wceq zcn negsub syl2an breq2d 3bitrd ) ACDZBCDZEZABFGABHZFGZAAUGIJZFG
    ZAABKJZFGABLUEUDUGCDUHUJPBMAUGNOUFUIUKAFUDAQDBQDUIUKRUEASBSABTUAUBUC $.
    $( [21-Mar-2011] $)

  $( An integer divides another iff it divides their difference.  (Contributed
     by Paul Chapman, 31-Mar-2011.) $)
  dvdssubr $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( N - M ) ) ) $=
    ( cz wcel wa cmin co cdivides wbr caddc simpl zsubcl ancoms dvdsadd syl2anc
    wb cc wceq zcn pncan3 syl2an breq2d bitr2d ) ACDZBCDZEZABAFGZHIZAAUGJGZHIZA
    BHIUFUDUGCDZUHUJPUDUEKUEUDUKBALMAUGNOUFUIBAHUDAQDBQDUIBRUEASBSABTUAUBUC $.
    $( [31-Mar-2011] $)

  ${
    dvdslelem.1 $e |- M e. ZZ $.
    dvdslelem.2 $e |- N e. NN $.
    dvdslelem.3 $e |- K e. ZZ $.
    $( Lemma for ~ dvdsle . $)
    dvdslelem $p |- ( N < M -> ( K x. M ) =/= N ) $=
      ( clt wbr cmul co wo cc0 cle cr wcel 0re wb wi wa ax-mp wne zrei lelttric
      c1 mp2an cz cn elnnz elnnz1 bitr3i pm5.32 orbi2i mpbi cneg le0neg1 nngt0i
      mpbir nnrei lttri mpan ltlei renegcli mulge0 mpanl12 sylan2 sylanb expcom
      syl an4s remulcli cc wceq recni mulneg1 breq2i bitr4i syl6ibr mp3an mpan2
      lelttr a1i syld lemulge12 sylan ex ltletr orim12d mpi lttri2i sylibr ) CB
      GHZABIJZCGHZCWLGHZKZWLCUAWKALMHZUDAMHZKZWOWPLAGHZKZWRANOZLNOZWTAFUBZPALUC
      UEWSWQWPAUFOZWSWQQZFXDXERXDWSSZXDWQSZQXFAUGOXGAUHAUIUJXDWSWQUKUQTULUMWKWP
      WMWQWNWKWPWLLMHZWMWKWPLAUNZBIJZMHZXHWPWKXKWPLXIMHZWKXKXAWPXLQXCAUOTWKXLLB
      MHZXKWKLBGHZXMLCGHZWKXNCEUPZLCBPCEURZBDUBZUSUTLBPXRVAVHZXINOZBNOZXLXMSXKA
      XCVBXRXTXLYAXMXKXIBVCVIVDVEVFVGXHLWLUNZMHZXKWLNOZXHYCQABXCXRVJZWLUOTXJYBL
      MAVKOBVKOXJYBVLAXCVMBXRVMABVNUEVOVPVQXHWMRWKXHXOWMXPYDXBCNOZXHXOSWMRYEPXQ
      WLLCVTVRVSWAWBWKWQBWLMHZWNWKWQYGWKXMWQYGXSYAXAXMWQSYGXRXCBAWCVDWDWEWKYGWN
      YFYAYDWKYGSWNRXQXRYECBWLWFVRWEWBWGWHWLCYEXQWIWJ $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d M n $.  $d N n $.
    $( The divisors of a positive integer are bounded by it.  The proof does
       not use ` / ` .  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdsle $p |- ( ( M e. ZZ /\ N e. NN ) -> ( M || N -> M <_ N ) ) $=
      ( vn cz wcel cn wa wbr clt wn cmul co wne wi c1 cif neeq1d imbi12d elimel
      wceq cdivides cle w3a cv wrex breq2 oveq2 breq1 neeq2 oveq1 imbi2d 1z 1nn
      dvdslelem dedth3h 3expia com23 3impia imp df-ne nrexdv nnz divides sylan2
      sylib wb 3adant3 mtbird con2d cr zre nnre lenlt syl2an sylibrd ) ADEZBFEZ
      GZABUAHZBAIHZJZABUBHZVRVTVSVPVQVTVSJVPVQVTUCZVSCUDZAKLZBTZCDUEZWCWFCDWCWD
      DEZGWEBMZWFJWCWHWIVPVQVTWHWINVRWHVTWIVPVQWHVTWINZVPVQWHWJBVPAOPZIHZWDWKKL
      ZBMZNVQBOPZWKIHZWMWOMZNWPWHWDOPZWKKLZWOMZNABWDOOOAWKTZVTWLWIWNAWKBIUFXAWE
      WMBAWKWDKUGQRBWOTWLWPWNWQBWOWKIUHBWOWMUIRWDWRTZWQWTWPXBWMWSWOWDWRWKKUJQUK
      WRWKWOAODULSBOFUMSWDODULSUNUOUPUQURUSWEBUTVEVAVPVQVSWGVFZVTVQVPBDEXCBVBCA
      BVCVDVGVHUPVIVPAVJEBVJEWBWAVFVQAVKBVLABVMVNVO $.
      $( [21-Mar-2011] $)
  $}

  $( The divisors of a nonzero integer are bounded by its absolute value.
     (Contributed by Paul Chapman, 21-Mar-2011.) $)
  dvdsleabs $p |- ( ( M e. ZZ /\ N e. ZZ /\ N =/= 0 ) ->
                    ( M || N -> M <_ ( abs ` N ) ) ) $=
    ( cz wcel cc0 wne w3a cdivides wbr cabs cfv wb wa wceq cneg wo adantl breq2
    cle wi cr zre absor syl a1i dvdsnegb adantr bitr4d jaod mpd 3adant3 nnabscl
    ex cn dvdsle sylan2 3impb sylbird ) ACDZBCDZBEFZGABHIZABJKZHIZAVCSIZUSUTVDV
    BLZVAUSUTMZVCBNZVCBOZNZPZVFUTVKUSUTBUADVKBUBBUCUDQVGVHVFVJVHVFTVGVCBAHRUEVG
    VJVFVGVJMVDAVIHIZVBVJVDVLLVGVCVIAHRQVGVBVLLVJABUFUGUHUMUIUJUKUSUTVAVDVETZUT
    VAMUSVCUNDVMBULAVCUOUPUQUR $.
    $( [21-Mar-2011] $)
  $( If two integers divide each other, they must be equal, up to a difference
     in sign. $)
  dvdseq $p |- ( ( ( M e. NN0 /\ N e. NN0 ) /\ ( M || N /\ N || M ) ) ->
                    M = N ) $=
    ( cn0 wcel wa cdivides wbr wceq cn cc0 wo wi elnn0 cle wb cr dvdsle syl2anc
    cz mpd nn0re nnre letri3 syl2an adantr simprl nn0z ad2antrr simplr ad2antlr
    simprr nnz wne wn nnne0 breq1 biimpcd ad2antrl 0dvds sylibd necon3ad simpll
    syl sylib ord mt3d mpbir2and ex simpr sylan9bbr eqeq2d bitr4d syl5ib jaodan
    sylan2b imp ) ACDZBCDZEABFGZBAFGZEZABHZVRVQBIDZBJHZKWAWBLZBMVQWCWEWDVQWCEZW
    AWBWFWAEZWBABNGZBANGZWFWBWHWIEOZWAVQAPDBPDWJWCAUABUBABUCUDUEWGVSWHWFVSVTUFW
    GASDZWCVSWHLVQWKWCWAAUGZUHVQWCWAUIABQRTWGVTWIWFVSVTUKWGBSDZAIDZVTWILWCWMVQW
    ABULUJZWGWNAJHZWGBJUMZWPUNWCWQVQWABUOUJWGWPBJWGWPJBFGZWDVSWPWRLWFVTWPVSWRAJ
    BFUPUQURWGWMWRWDOWOBUSVCUTVATWGWNWPWGVQWNWPKVQWCWAVBAMVDVEVFBAQRTVGVHWAVTVQ
    WDEZWBVSVTVIWSVTWPWBWDVTJAFGZVQWPBJAFUPVQWKWTWPOWLAUSVCVJWSBJAVQWDVIVKVLVMV
    NVOVP $.
    $( [30-May-2014] $)

  ${
    $d N x $.
    $( Only 0 is divisible by all integers.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    alzdvds $p |- ( N e. ZZ -> ( A. x e. ZZ x || N <-> N = 0 ) ) $=
      ( cz wcel cv cdivides wbr wral cc0 wceq wa wne wn wrex cr cn syl ralrimiv
      wi expcom cabs cfv cle clt zcn abscl arch wss nnssz ssrexv ax-mp 3syl zre
      cc ltnle syl2an rexbidva rexnal syl6bb mpbid adantl dvdsleabs 3expb ralim
      wb com12 expdimp mtod nne sylib dvds0 breq2 syl5ibr impbid1 ) BCDZAEZBFGZ
      ACHZBIJZVRVOVSVRVOKZBILZMVSVTWAVPBUAUBZUCGZACHZVOWDMZVRVOWBVPUDGZACNZWEVO
      WBODZWFAPNZWGVOBUNDWHBUEBUFQZWBAUGPCUHWIWGSUIWFAPCUJUKULVOWGWCMZACNWEVOWF
      WKACVOWHVPODWFWKVEVPCDZWJVPUMWBVPUOUPUQWCACURUSUTVAVRVOWAWDVOWAKZVRWDWMVQ
      WCSZACHVRWDSWMWNACWLWMWNWLVOWAWNVPBVBVCTRVQWCACVDQVFVGVHBIVIVJTVSVQACWLVQ
      VSVPIFGVPVKBIVPFVLVMRVN $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K x y $.  $d N x $.
    $( A positive integer divides any greater factorial.  (Contributed by Paul
       Chapman, 28-Nov-2012.) $)
    dvdsfac $p |- ( ( K e. NN /\ N e. ( ZZ>= ` K ) ) -> K || ( ! ` N ) ) $=
      ( vx vy cfv wcel cn cfa cdivides wbr wi c1 co wceq fveq2 breq2d imbi2d cz
      nnz 3syl cuz cv caddc cmin cmul cn0 nnm1nn0 faccl dvdsmul2 syl2anc facnn2
      mpbird a1i wa adantl elnnuz uztrn sylan2b sylibr nnnn0 peano2z dvdsmultr1
      syl syl3anc facp1 sylibrd ex a2d uzind4 impcom ) BAUAEZFAGFZABHEZIJZVLACU
      BZHEZIJZKVLAAHEZIJZKZVLADUBZHEZIJZKVLAWALUCMZHEZIJZKVLVNKCDABVOANZVQVSVLW
      GVPVRAIVOAHOPQVOWANZVQWCVLWHVPWBAIVOWAHOPQVOWDNZVQWFVLWIVPWEAIVOWDHOPQVOB
      NZVQVNVLWJVPVMAIVOBHOPQVTARFZVLVSAALUDMZHEZAUEMZIJZVLWMRFZWKWOVLWLUFFWMGF
      WPAUGWLUHWMSTASZWMAUIUJVLVRWNAIAUKPULUMWAVKFZVLWCWFWRVLWCWFKWRVLUNZWCAWBW
      DUEMZIJZWFWSWKWBRFZWDRFZWCXAKVLWKWRWQUOWSWBGFZXBWSWAGFZWAUFFZXDWSWALUAEZF
      ZXEVLWRAXGFXHAUPAWALUQURWAUPUSZWAUTZWAUHTWBSVCWSXEWARFXCXIWASWAVATAWBWDVB
      VDWSWEWTAIWSXEXFWEWTNXIXJWAVETPVFVGVHVIVJ $.
      $( [28-Nov-2012] $)
  $}

  ${
    $d K x y $.  $d N x $.
    $( A power divides a power with a greater exponent.  (Contributed by Mario
       Carneiro, 23-Feb-2014.) $)
    dvdsexp $p |- ( ( A e. ZZ /\ M e. NN0 /\ N e. ( ZZ>= ` M ) ) ->
                    ( A ^ M ) || ( A ^ N ) ) $=
      ( cz wcel cn0 cuz cfv w3a cexp co cdivides wbr 3ad2ant3 zexpcl syl2anc cc
      wceq zcn syl cmin cmul simp1 cle eluz2 znn0sub2 sylbi 3adant3 caddc simp2
      dvdsmul2 expadd syl3anc eluzelz nn0cn npcan oveq2d eqtr3d breqtrd ) ADEZB
      FEZCBGHEZIZABJKZACBUAKZJKZVDUBKZACJKZLVCVFDEZVDDEZVDVGLMVCUTVEFEZVIUTVAVB
      UCZVBUTVKVAVBBDECDEZBCUDMIVKBCUEBCUFUGNZAVEOPUTVAVJVBABOUHVFVDUKPVCAVEBUI
      KZJKZVGVHVCAQEZVKVAVPVGRVCUTVQVLASTVNUTVAVBUJZAVEBULUMVCVOCAJVCCQEZBQEZVO
      CRVBUTVSVAVBVMVSBCUNCSTNVCVAVTVRBUOTCBUPPUQURUS $.
      $( [23-Feb-2014] $)
  $}

