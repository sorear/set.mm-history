$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Initial properties of the complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Identity law for multiplication.  Note: see ~ mulid1 for commuted
     version. $)
  mulid2 $p |- ( A e. CC -> ( 1 x. A ) = A ) $=
    ( cc wcel c1 cmul co wceq ax-1cn mulcom mpan mulid1 eqtrd ) ABCZDAEFZADEFZA
    DBCMNOGHDAIJAKL $.
    $( [8-Oct-1999] $)

  $( Commutative/associative law for multiplication. $)
  mul12 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A x. ( B x. C ) ) = ( B x. ( A x. C ) ) ) $=
    ( cc wcel w3a cmul co wceq wa mulcom oveq1d 3adant3 mulass 3com12 3eqtr3d )
    ADEZBDEZCDEZFABGHZCGHZBAGHZCGHZABCGHGHBACGHGHZQRUAUCISQRJTUBCGABKLMABCNRQSU
    CUDIBACNOP $.
    $( [30-Apr-2005] $)

  $( Commutative/associative law. $)
  mul32 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A x. B ) x. C ) = ( ( A x. C ) x. B ) ) $=
    ( cc wcel w3a cmul co wceq wa mulcom oveq2d 3adant1 mulass 3com23 3eqtr4d )
    ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCKLMABCNQSRU
    DUCIACBNOP $.
    $( [8-Oct-1999] $)

  $( Commutative/associative law. $)
  mul31 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A x. B ) x. C ) = ( ( C x. B ) x. A ) ) $=
    ( cc wcel w3a cmul co wceq wa ax-mulcom oveq2d mulass ax-mulcl ancoms simp1
    3adant1 mulcom syl2anc 3eqtr4d ) ADEZBDEZCDEZFZABCGHZGHZACBGHZGHZABGHCGHUGA
    GHZUBUCUFUHIUAUBUCJUEUGAGBCKLQABCMUDUGDEZUAUIUHIUBUCUJUAUCUBUJCBNOQUAUBUCPU
    GARST $.
    $( [3-Jan-2013] $)

  $( Rearrangement of 4 factors. $)
  mul4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A x. B ) x. ( C x. D ) ) =
                 ( ( A x. C ) x. ( B x. D ) ) ) $=
    ( cc wcel wa cmul co wceq w3a mul32 oveq1d 3expa adantrr mulcl mulass 3expb
    sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZBHIZDHIZUHCDHIH
    IZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUDUHEFZUGUJUNJZABPUQUEUF
    URUHCDQRSUBUEUCUFUMUOJZUBUEGUKEFZUCUFGUSACPUTUCUFUSUKBDQRSTUA $.
    $( [8-Oct-1999] $)

  $( A simple product of sums expansion. $)
  muladd11 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( 1 + A ) x. ( 1 + B ) ) =
                 ( ( 1 + A ) + ( B + ( A x. B ) ) ) ) $=
    ( cc wcel wa c1 caddc cmul wceq ax-1cn addcl mpan adddi mp3an2 sylan mulid1
    co syl adantr eqtrd adddir mp3an1 mulid2 adantl oveq1d oveq12d ) ACDZBCDZEZ
    FAGQZFBGQHQZUJFHQZUJBHQZGQZUJBABHQZGQZGQUGUJCDZUHUKUNIZFCDZUGUQJFAKLZUQUSUH
    URJUJFBMNOUIULUJUMUPGUGULUJIZUHUGUQVAUTUJPRSUIUMFBHQZUOGQZUPUSUGUHUMVCIJFAB
    UAUBUIVBBUOGUHVBBIUGBUCUDUETUFT $.
    $( [21-Feb-2005] $)

  ${
    mul.1 $e |- A e. CC $.
    mul.2 $e |- B e. CC $.
    mul.3 $e |- C e. CC $.
    $( Commutative/associative law that swaps the first two factors in a triple
       product.  (The proof was shortened by Andrew Salmon, 19-Nov-2011.) $)
    mul12i $p |- ( A x. ( B x. C ) ) = ( B x. ( A x. C ) ) $=
      ( cc wcel cmul co wceq mul12 mp3an ) AGHBGHCGHABCIJIJBACIJIJKDEFABCLM $.
      $( [19-Nov-2011] $) $( [11-May-1999] $)

    $( Commutative/associative law that swaps the last two factors in a triple
       product. $)
    mul32i $p |- ( ( A x. B ) x. C ) = ( ( A x. C ) x. B ) $=
      ( cc wcel cmul co wceq mul32 mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM $.
      $( [11-May-1999] $)
  $}

  ${
    mul4.1 $e |- A e. CC $.
    mul4.2 $e |- B e. CC $.
    mul4.3 $e |- C e. CC $.
    mul4.4 $e |- D e. CC $.
    $( Rearrangement of 4 factors. $)
    mul4i $p |- ( ( A x. B ) x. ( C x. D ) ) =
               ( ( A x. C ) x. ( B x. D ) ) $=
      ( cc wcel cmul co wceq mul4 mp4an ) AIJBIJCIJDIJABKLCDKLKLACKLBDKLKLMEFGH
      ABCDNO $.
      $( [16-Feb-1995] $)
  $}

  ${
    1p1timesi.1 $e |- A e. CC $.
    $( Two times a number. $)
    1p1timesi $p |- ( ( 1 + 1 ) x. A ) = ( A + A ) $=
      ( c1 caddc co cmul ax-1cn adddiri mulid2i oveq12i eqtri ) CCDEAFECAFEZLDE
      AADECCAGGBHLALADABIZMJK $.
      $( [18-May-1999] $)
  $}

  $( A theorem for reals analogous the second Peano postulate ~ peano2nn . $)
  peano2re $p |- ( A e. RR -> ( A + 1 ) e. RR ) $=
    ( cr wcel c1 caddc co 1re readdcl mpan2 ) ABCDBCADEFBCGADHI $.
    $( [5-Jul-2005] $)

  ${
    readdcani.1 $e |- A e. RR $.
    readdcani.2 $e |- B e. RR $.
    readdcani.3 $e |- C e. RR $.
    $( Cancellation law for addition over the reals.  (Contributed by Scott
       Fenton, 3-Jan-2013.) $)
    readdcani $p |- ( ( C + A ) = ( C + B ) <-> A = B ) $=
      ( caddc co wceq clt wbr readdcli ltnri breq1 mtbiri ltadd2i sylnibr breq2
      wn lttri3i sylanbrc oveq2 impbii ) CAGHZCBGHZIZABIZUFABJKZSBAJKZSUGUFUDUE
      JKZUHUFUJUEUEJKZUECBFELMZUDUEUEJNOABCDEFPQUFUEUDJKZUIUFUMUKULUDUEUEJROBAC
      EDFPQABDETUAABCGUBUC $.
      $( [3-Jan-2013] $)
  $}

  $( Cancellation law for addition over the reals. $)
  readdcan $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( C + A ) = ( C + B ) <-> A = B ) ) $=
    ( cr wcel caddc co wceq wb c1 oveq2 eqeq1d eqeq1 bibi12d eqeq2d eqeq2 oveq1
    cif 1re elimel eqeq12d bibi1d readdcani dedth3h ) ADEZBDEZCDEZCAFGZCBFGZHZA
    BHZICUEAJRZFGZUIHZULBHZIUMCUFBJRZFGZHZULUPHZIUGCJRZULFGZUTUPFGZHZUSIABCJJJA
    ULHZUJUNUKUOVDUHUMUIAULCFKLAULBMNBUPHZUNURUOUSVEUIUQUMBUPCFKOBUPULPNCUTHZUR
    VCUSVFUMVAUQVBCUTULFQCUTUPFQUAUBULUPUTAJDSTBJDSTCJDSTUCUD $.
    $( [3-Jan-2013] $)

  ${
    $d c y $.
    $( ` 0 ` is its own additive identity.  (Contributed by Scott Fenton,
       3-Jan-2013.) $)
    00id $p |- ( 0 + 0 ) = 0 $=
      ( vc vy cc0 cr wcel cv caddc co wceq 0re wa cmul c1 cc 0cn syl2anc eqtr3d
      recnd oveq1d remulcl wrex ax-rnegex oveq2 eqeq1d biimpd adantld ax-rrecex
      wi wne adantlr simplll simprl mulass mp3an3 oveq1 mulid2i syl6eq ad2antll
      simpllr mpan2 ad2antrl adddir mp3an1 sylancr addass eqtr2d sylan2 readdcl
      wb sylancl readdcan mp3an2 mpbid exp32 rexlimdv mpd expcom pm2.61ine mp2b
      rexlimiva ) CDEZCAFZGHZCIZADUACCGHZCIZJACUBWDWFADWBDEZWDKZWFUHWBCWBCIZWDW
      FWGWIWDWFWIWCWECWBCCGUCUDUEUFWHWBCUIZWFWHWJKZWBBFZLHZMIZBDUAZWFWGWJWOWDBW
      BUGUJWKWNWFBDWKWLDEZWNWFWKWPWNKZKZWBWLCLHZLHZCGHZWECWRWTCCGWRWMCLHZWTCWRW
      BNEZWLNEZXBWTIZWRWBWGWDWJWQUKZRZWRWLWKWPWNULZRXCXDCNEZXEOWBWLCUMUNPWNXBCI
      WKWPWNXBMCLHCWMMCLUOCOUPUQURQSWRCWSLHZXAGHZXJCGHZIZXACIZWRXLXJWTGHZCGHZXK
      WRXJXOCGWRWCWSLHZXJXOWRWCCWSLWGWDWJWQUSSWRXCWSNEZXQXOIZXGWRWSWPWSDEZWKWNW
      PWAXTJWLCTUTZVAZRXIXCXRXSOCWBWSVBVCPQSWRXJNEZWTNEZXPXKIZWRXJWPXJDEZWKWNWP
      WAXTYFJYACWSTVDVAZRWRWTWRWGXTWTDEZXFYBWBWSTZPRYCYDXIYEOXJWTCVEUNPVFWRXADE
      ZYFXMXNVIZWRWGWPYJXFXHWGWPKYHWAYJWPWGXTYHYAYIVGJWTCVHVJPYGYJWAYFYKJXACXJV
      KVLPVMQVNVOVPVQVRVTVS $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Lemma for ~ mul02 .  If any real does not produce ` 0 ` when multiplied
       by ` 0 ` , then any complex is equal to double itself. $)
    mul02lem1 $p |- ( ( ( A e. RR /\ -. ( 0 x. A ) = 0 ) /\ B e. CC ) ->
                       B = ( B + B ) ) $=
      ( vy cr wcel cc0 cmul co wn wa cc c1 caddc recnd mulcl syl2anc 0cn mp3an3
      wceq eqtrd cv wrex wne 0re ax-mulrcl mpan biimpri ax-rrecex syl2an adantr
      df-ne 00id oveq2i eqcomi simprl simplll simplr mul32 simprr oveq1d mulid2
      mul31 ad2antlr adddi mp3an23 syl oveq12d eqeq12d mpbii exp32 rexlimdv mpd
      3eqtrd ) ADEZFAGHZFSIZJZBKEZJZVOCUAZGHZLSZCDUBZBBBMHZSZVQWCVRVNVODEZVOFUC
      ZWCVPFDEVNWFUDFAUEUFWGVPVOFUKUGCVOUHUIUJVSWBWECDVSVTDEZWBWEVSWHWBJZJZVTAG
      HZBGHZFGHZWLFFMHZGHZSWEWOWMWNFWLGULUMUNWJWMBWOWDWJWMWKFGHZBGHZLBGHZBWJWKK
      EZVRWMWQSZWJVTKEZAKEZWSWJVTVSWHWBUONZWJAVNVPVRWIUPNZVTAOPZVQVRWIUQZWSVRFK
      EZWTQWKBFURRPWJWPLBGWJWPWALWJXAXBWPWASZXCXDXAXBXGXHQVTAFVBRPVSWHWBUSTUTVR
      WRBSVQWIBVAVCVMZWJWOWMWMMHZWDWJWLKEZWOXJSZWJWSVRXKXEXFWKBOPXKXGXGXLQQWLFF
      VDVEVFWJWMBWMBMXIXIVGTVHVIVJVKVL $.
      $( [3-Jan-2013] $)
  $}

  $( Lemma for ~ mul02 .  Zero times a real is zero. $)
  mul02lem2 $p |- ( A e. RR -> ( 0 x. A ) = 0 ) $=
    ( cr wcel cc0 cmul co wceq ci c1 caddc wne wn ax-1ne0 ax-i2m1 oveq1i eqtr4i
    00id ax-icn ax-1cn 0re eqeq12i eqcom mulcli eqeq2i bitri readdcani 3bitr3ri
    addassi 1re necon3abii mpbi wa cc mul02lem1 mpan2 oveq2d ex mt3i ) ABCZDAEF
    DGZHHEFZIJFZVAIIJFZJFZGZIDKVELMVEIDVBIJFZVBGZDIJFZDDJFZGVEIDGVFVHVBVIVBDIJN
    OVBDVINQPUAVGVBVFGVEVFVBUBVFVDVBVAIIHHRRUCSSUHUDUEIDDUITTUFUGUJUKUSUTLZVEUS
    VJULZIVCVAJVKIUMCIVCGSAIUNUOUPUQUR $.
    $( [3-Jan-2013] $)

  ${
    $d A x y $.
    $( Multiplication by ` 0 ` .  Theorem I.6 of [Apostol] p. 18.  Based on
       ideas by Eric Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
    mul02 $p |- ( A e. CC -> ( 0 x. A ) = 0 ) $=
      ( vx vy cc wcel cv ci cmul co caddc wceq cr wrex cc0 ax-icn 0cn mul02lem2
      recn syl eqtrd cnre wa mulcl sylancr adddi mp3an1 mul12 mp3an12 oveqan12d
      syl2an oveq2d cnrei oveq2 eqeq1d syl5ibrcom rexlimivv ax-mp eqtr3i syl6eq
      0re ) ADEABFZGCFZHIZJIZKZCLMBLMNAHIZNKZBCAUAVEVGBCLLVALEZVBLEZUBZVGVENVDH
      IZNKVJVKNGNHIZJIZNVJVKNVAHIZNVCHIZJIZVMVHVADEZVCDEZVKVPKZVIVARVIGDEZVBDEZ
      VROVBRZGVBUCUDNDEZVQVRVSPNVAVCUEUFUJVHVIVNNVOVLJVAQVIVOGNVBHIZHIZVLVIWAVO
      WEKZWBWCVTWAWFPONGVBUGUHSVIWDNGHVBQUKTUITZNNHIZVMNNVDKZCLMBLMWHVMKZBCNPUL
      WIWJBCLLVJWJWIVKVMKWGWIWHVKVMNVDNHUMUNUOUPUQNLEWHNKUTNQUQURUSVEVFVKNAVDNH
      UMUNUOUPS $.
      $( [3-Jan-2013] $) $( [10-Aug-1999] $)
  $}

  $( Multiplication by ` 0 ` .  Theorem I.6 of [Apostol] p. 18. $)
  mul01 $p |- ( A e. CC -> ( A x. 0 ) = 0 ) $=
    ( cc wcel cc0 cmul co wceq 0cn mulcom mpan2 mul02 eqtrd ) ABCZADEFZDAEFZDMD
    BCNOGHADIJAKL $.
    $( [3-Jan-2013] $) $( [15-May-1999] $)

  ${
    mulzer.1 $e |- A e. CC $.
    $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
    mul02i $p |- ( 0 x. A ) = 0 $=
      ( cc wcel cc0 cmul co wceq mul02 ax-mp ) ACDEAFGEHBAIJ $.
      $( [23-Nov-1994] $)

    $( Multiplication by ` 0 ` .  Theorem I.6 of [Apostol] p. 18. $)
    mul01i $p |- ( A x. 0 ) = 0 $=
      ( cc wcel cc0 cmul co wceq mul01 ax-mp ) ACDAEFGEHBAIJ $.
      $( [3-Jan-2013] $) $( [23-Nov-1994] $)
  $}

  ${
    $d A c x $.
    $( ` 0 ` is an additive identity.  This used to be one of our complex
       number axioms, until it was found to be dependent on the others.  Based
       on ideas by Eric Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
    addid1 $p |- ( A e. CC -> ( A + 0 ) = A ) $=
      ( vc vx c1 cr wcel caddc co cc0 wceq cc wi 1re wa ax-1cn 0cn 0re oveq2 ci
      cmul cv wrex ax-rnegex wne 00id recn addass mp3an13 oveq1 sylan9req simpr
      syl 3eqtr4a wb readdcl mpan2 readdcan mp3an3 mpancom adantr mpbid ax-1ne0
      eqeq1d biimpcd ax-icn mulcli adddii mulid1i mul01i ax-i2m1 eqtr4i oveq12i
      eqtri eqeq12i oveq2i 3eqtr3ri oveq1i eqtr3i eqcomi readdcani 3bitri sylib
      addassi syl6 necon3d mpi adantl jca ex ax-rrecex simp3 simp2l recnd mulcl
      w3a syl2anc simp1l adddi mul31 simp2r oveq1d mulid2 3ad2ant3 3eqtrd mul01
      syl3anc oveq12d eqtrd eqeq12d 3exp exp3a rexlimdv mpd com34 imp3a ancomsd
      syl5ib syld rexlimiv mp2b ) DEFZDBUAZGHZIJZBEUBAKFZAIGHZAJZLZMBDUCYDYHBEY
      BEFZYDYBIGHZYBJZYBIUDZNZYHYIYDYMYIYDNZYKYLYNDYJGHZYCJZYKYNIIGHZIYOYCUEYIY
      DYOYCIGHZYQYIYBKFZYRYOJZYBUFDKFYSIKFZYTOPDYBIUGUHULYCIIGUIUJYIYDUKUMYIYPY
      KUNZYDYJEFZYIUUBYIIEFUUCQYBIUOUPUUCYIYAUUBMYJYBDUQURUSUTVAYDYLYIYDDIUDYLV
      BYDYBIDIYDYBIJZDIGHZIJZDIJZUUDYDUUFUUDYCUUEIYBIDGRVCVDUUFSSTHZUUHTHZUUETH
      ZUUIITHZJZUUGUUEIUUITRUULUUIUUHDGHZGHZIJIDGHZYQJUUGUUJUUNUUKIUUJUUIDTHZUU
      KGHUUNUUIDIUUHUUHSSVEVEVFZUUQVFZOPVGUUPUUIUUKUUMGUUIUURVHUUKIUUMUUIUURVIZ
      VJVKVLVMUUSVNUUNUUOIYQUUIUUHGHZDGHUUNUUOUUIUUHDUURUUQOWCUUTIDGUUHUUMTHZUU
      IUUHDTHZGHIUUTUUHUUHDUUQUUQOVGUVAUUHITHIUUMIUUHTVJVOUUHUUQVIVMUVBUUHUUIGU
      UHUUQVHVOVPVQVRYQIUEVSVNDIIMQQVTWAWBWDWEWFWGWHWIYIYLYKYHYIYLYKYHYIYLYEYKY
      GYIYLYEYKYGLZLZYIYLNZYBCUAZTHZDJZCEUBUVDCYBWJUVEUVHUVDCEUVEUVFEFZUVHUVDUV
      EUVIUVHNZYEUVCYKAUVFTHZYJTHZUVKYBTHZJUVEUVJYEWOZYGYJYBUVKTRUVNUVLYFUVMAUV
      NUVLUVMUVKITHZGHZYFUVNUVKKFZYSUVLUVPJZUVNYEUVFKFZUVQUVEUVJYEWKZUVNUVFUVEU
      VIUVHYEWLWMZAUVFWNWPZUVNYBYIYLUVJYEWQWMZUVQYSUUAUVRPUVKYBIWRURWPUVNUVMAUV
      OIGUVNUVMUVGATHZDATHZAUVNYEUVSYSUVMUWDJUVTUWAUWCAUVFYBWSXFUVNUVGDATUVEUVI
      UVHYEWTXAYEUVEUWEAJUVJAXBXCXDZUVNUVQUVOIJUWBUVKXEULXGXHUWFXIXQXJXKXLXMWIX
      NXOXPXRXSXT $.
      $( [3-Jan-2013] $)
  $}

  ${
    addid1i.1 $e |- A e. CC $.
    $( ` 0 ` is an additive identity. $)
    addid1i $p |- ( A + 0 ) = A $=
      ( cc wcel cc0 caddc co wceq addid1 ax-mp ) ACDAEFGAHBAIJ $.
      $( [3-Jan-2013] $) $( [23-Nov-1994] $)
  $}

  ${
    $d A x a b c d $.
    $( Existence of the negative of a complex number.  (Contributed by Eric
       Schmidt, 21-May-2007.)  (Revised by Scott Fenton, 3-Jan-2013). $)
    cnegex $p |- ( A e. CC -> E. x e. CC ( A + x ) = 0 ) $=
      ( va vb vc vd cc wcel cv ci cmul co caddc wceq cr wrex cc0 wa ax-icn recn
      cnre ax-rnegex anim12i reeanv sylibr mulcl sylancr addcl syl2anr ad2antlr
      ad2antrr syl2anc ad2antll addass syl3anc oveq1d eqtr3d adantr adddi oveq2
      ad2antrl mp3an1 mul01i syl6eq adantl oveq2d ad3antrrr addid1 eqtrd eqeq1d
      syl exbiri com23 imp32 rcla4ev exp31 rexlimdvv oveq1 syl5ibrcom rexlimivv
      mpd rexbidv ) BGHBCIZJDIZKLZMLZNZDOPCOPBAIZMLZQNZAGPZCDBUAWGWKCDOOWCOHZWD
      OHZRZWKWGWFWHMLZQNZAGPZWNWCEIZMLZQNZWDFIZMLZQNZRZFOPEOPZWQWNWTEOPZXCFOPZR
      XEWLXFWMXGEWCUBFWDUBUCWTXCEFOOUDUEWNXDWQEFOOWNWROHZXAOHZRZXDWQWNXJRZXDRZJ
      XAKLZWRMLZGHZWFXNMLZQNZWQXJXOWNXDXIXMGHZWRGHZXOXHXIJGHZXAGHZXRSXATZJXAUFZ
      UGWRTZXMWRUHUIUJXLXPWCWEXMMLZMLZWRMLZQXKXPYGNXDXKWFXMMLZWRMLZXPYGXKWFGHZX
      RXSYIXPNXKWCGHZWEGHZYJWLYKWMXJWCTZUKZXKXTWDGHZYLSWMYOWLXJWDTUJZJWDUFUGZWC
      WEUHULXKXTYAXRSXIYAWNXHYBUMZYCUGZXHXSWNXIYDVAWFXMWRUNUOXKYHYFWRMXKYKYLXRY
      HYFNYNYQYSWCWEXMUNUOUPUQURXKWTXCYGQNZXKXCWTYTXKXCYTWTXKXCRZYGWSQUUAYFWCWR
      MUUAYFWCQMLZWCUUAYEQWCMUUAJXBKLZYEQUUAYOYAUUCYENZXKYOXCYPURXKYAXCYRURXTYO
      YAUUDSJWDXAUSVBULXCUUCQNXKXCUUCJQKLQXBQJKUTJSVCVDVEUQVFUUAYKUUBWCNWLYKWMX
      JXCYMVGWCVHVKVIUPVJVLVMVNVIWPXQAXNGWHXNNWOXPQWHXNWFMUTVJVOULVPVQWAWGWJWPA
      GWGWIWOQBWFWHMVRVJWBVSVTVK $.
      $( [3-Jan-2013] $) $( [21-May-2007] $)
  $}

  ${
    $d A x $.
    $( Existence of a left inverse for addition.  (Contributed by Scott Fenton,
       3-Jan-2013.) $)
    cnegex2 $p |- ( A e. CC -> E. x e. CC ( x + A ) = 0 ) $=
      ( cc wcel ci cmul co caddc cc0 wceq cv wrex ax-icn mulcli mulcl c1 ax-1cn
      mpan adddir mp3an12 ax-i2m1 oveq1i syl5eq mulid2 3eqtr3rd rcla4ev syl2anc
      mul02 oveq2d oveq1 eqeq1d ) BCDZEEFGZBFGZCDZUNBHGZIJZAKZBHGZIJZACLUMCDZUL
      UOEEMMNZUMBORULUMPHGZBFGZUNPBFGZHGZIUPVAPCDULVDVFJVBQUMPBSTULVDIBFGIVCIBF
      UAUBBUHUCULVEBUNHBUDUIUEUTUQAUNCURUNJUSUPIURUNBHUJUKUFUG $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x y $.
    $( ` 0 ` is a left identity for addition.  This used to be one of our
       complex number axioms, until it was discovered that it was dependent on
       the others.  Based on ideas by Eric Schmidt.  (Contributed by Scott
       Fenton, 3-Jan-2013.) $)
    addid2 $p |- ( A e. CC -> ( 0 + A ) = A ) $=
      ( vx vy cc wcel cv caddc co cc0 wceq wrex cnegex ad2antrl addass 3eqtr3rd
      wa oveq2d 0cn rexlimdv mpd w3a simp2l simp3l syl3anc simp2r oveq1d simp3r
      simp1 addid1 3ad2ant1 oveq1i mp3an12 adantr 3ad2ant3 eqtr3d eqtrd syl5eqr
      00id 3expia exp3a exp32 ) ADEZABFZGHZIJZBDKIAGHZAJZBALVBVEVGBDVBVCDEZVEVG
      VBVHVEPZPZVCCFZGHZIJZCDKZVGVHVNVBVECVCLMVJVMVGCDVJVKDEZVMVGVBVIVOVMPZVGVB
      VIVPUAZAIGHZIVKGHZAVFVQVDVKGHZAVLGHZVSVRVQVBVHVOVTWAJVBVIVPUHVBVHVEVPUBVB
      VIVOVMUCAVCVKNUDVQVDIVKGVBVHVEVPUEUFVQVLIAGVBVIVOVMUGQOZVBVIVRAJVPAUIUJZV
      QVSIIGHZVKGHZVFWDIVKGURUKVQWEIVSGHZVFVPVBWEWFJZVIVOWGVMIDEZWHVOWGRRIIVKNU
      LUMUNVQVSAIGVQVRVSAWBWCUOQUPUQOUSUTSTVAST $.
      $( [3-Jan-2013] $)
  $}

  ${
    addid2i.1 $e |- A e. CC $.
    $( ` 0 ` is a left identity for addition. $)
    addid2i $p |- ( 0 + A ) = A $=
      ( cc wcel cc0 caddc co wceq addid2 ax-mp ) ACDEAFGAHBAIJ $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    addcani.1 $e |- A e. CC $.
    addcani.2 $e |- B e. CC $.
    addcani.3 $e |- C e. CC $.
    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18. $)
    addcani $p |- ( ( A + B ) = ( A + C ) <-> B = C ) $=
      ( vx caddc co wceq cc0 cc cv oveq2 addass mp3an23 oveq1 sylan9req addid2i
      wcel wrex wi cnegex2 wa eqeq12d syl5ib rexlimiva mp2b 3eqtr3g impbii ) AB
      HIZACHIZJZBCJUMKBHIZKCHIZBCALTZGMZAHIZKJZGLUAUMUNUOJZUBZDGAUCUSVAGLUMUQUK
      HIZUQULHIZJUQLTZUSUDZUTUKULUQHNVEVBUNVCUOVDUSVBURBHIZUNVDUPBLTVFVBJDEUQAB
      OPURKBHQRVDUSVCURCHIZUOVDUPCLTVGVCJDFUQACOPURKCHQRUEUFUGUHBESCFSUIBCAHNUJ
      $.
      $( [3-Jan-2013] $)   $( [27-Oct-1999] $)

    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18. $)
    addcan2i $p |- ( ( A + C ) = ( B + C ) <-> A = B ) $=
      ( vx caddc co wceq cc wcel cv oveq1 addass mp3an12 oveq2 sylan9eq addid1i
      cc0 wrex wi cnegex wa eqeq12d syl5ib rexlimiva mp2b 3eqtr3g impbii ) ACHI
      ZBCHIZJZABJUMATHIZBTHIZABCKLZCGMZHIZTJZGKUAUMUNUOJZUBZFGCUCUSVAGKUMUKUQHI
      ZULUQHIZJUQKLZUSUDZUTUKULUQHNVEVBUNVCUOVDUSVBAURHIZUNAKLUPVDVBVFJDFACUQOP
      URTAHQRVDUSVCBURHIZUOBKLUPVDVCVGJEFBCUQOPURTBHQRUEUFUGUHADSBESUIABCHNUJ
      $.
      $( [3-Jan-2013] $)   $( [14-May-2003] $)
  $}

  $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18.  This
     proof illustrates how ~ dedth3h can be used to convert the assumptions of
     ~ addcani into antecedents.  This general method can be used to convert
     deductions into theorems as needed. $)
  addcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A + B ) = ( A + C ) <-> B = C ) ) $=
    ( cc wcel caddc co wceq wb cc0 cif oveq1 eqeq12d bibi1d oveq2 eqeq1 bibi12d
    eqeq1d 0cn elimel eqeq2d eqeq2 addcani dedth3h ) ADEZBDEZCDEZABFGZACFGZHZBC
    HZIUEAJKZBFGZULCFGZHZUKIULUFBJKZFGZUNHZUPCHZIUQULUGCJKZFGZHZUPUTHZIABCJJJAU
    LHZUJUOUKVDUHUMUIUNAULBFLAULCFLMNBUPHZUOURUKUSVEUMUQUNBUPULFORBUPCPQCUTHZUR
    VBUSVCVFUNVAUQCUTULFOUACUTUPUBQULUPUTAJDSTBJDSTCJDSTUCUD $.
    $( [3-Jan-2013] $)   $( [22-Nov-1994] $)

  $( Cancellation law for addition. $)
  addcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A + C ) = ( B + C ) <-> A = B ) ) $=
    ( cc wcel caddc co wb cc0 cif oveq1 eqeq1d eqeq1 bibi12d eqeq2d eqeq2 oveq2
    wceq 0cn elimel eqeq12d bibi1d addcan2i dedth3h ) ADEZBDEZCDEZACFGZBCFGZRZA
    BRZHUEAIJZCFGZUIRZULBRZHUMUFBIJZCFGZRZULUPRZHULUGCIJZFGZUPUTFGZRZUSHABCIIIA
    ULRZUJUNUKUOVDUHUMUIAULCFKLAULBMNBUPRZUNURUOUSVEUIUQUMBUPCFKOBUPULPNCUTRZUR
    VCUSVFUMVAUQVBCUTULFQCUTUPFQUAUBULUPUTAIDSTBIDSTCIDSTUCUD $.
    $( [3-Jan-2013] $)    $( [30-Jul-2004] $)

  ${
    addcomi.1 $e |- A e. CC $.
    addcomi.2 $e |- B e. CC $.
    $( Addition commutes.  Based on ideas by Eric Schmidt.  (Contributed by
       Scott Fenton, 3-Jan-2013.) $)
    addcomi $p |- ( A + B ) = ( B + A ) $=
      ( caddc co wceq cmul ax-1cn addcli adddiri mulid1i oveq12i eqtri 3eqtr3ri
      c1 adddii addassi 3eqtr4i mpbi addcan2i 3eqtr3i addcani ) AABEFZEFZABAEFZ
      EFZGUDUFGAAEFZBEFZUDAEFZUEUGUIBEFZUJBEFZGUIUJGUHBBEFZEFZUDUDEFZUKULUDPPEF
      ZHFZAUPHFZBUPHFZEFUOUNABUPCDPPIIJKUQUDPHFZUTEFUOUDPPABCDJZIIQUTUDUTUDEUDV
      ALZVBMNURUHUSUMEURAPHFZVCEFUHAPPCIIQVCAVCAEACLZVDMNUSBPHFZVEEFUMBPPDIIQVE
      BVEBEBDLZVFMNMOUHBBAACCJZDDRUDABVACDRSUIUJBUHBVGDJUDAVACJDUATAABCCDRABACD
      CRUBAUDUFCVABADCJUCT $.
      $( [3-Jan-2013] $)
  $}

  $( Addition commutes.  This used to be one of our complex number axioms,
     until it was found to be dependent on the others.  Based on ideas by Eric
     Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
  addcom $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) = ( B + A ) ) $=
    ( cc wcel caddc wceq cc0 cif oveq1 oveq2 eqeq12d 0cn elimel addcomi dedth2h
    co ) ACDZBCDZABEPZBAEPZFQAGHZBEPZBUAEPZFUARBGHZEPZUDUAEPZFABGGAUAFSUBTUCAUA
    BEIAUABEJKBUDFUBUEUCUFBUDUAEJBUDUAEIKUAUDAGCLMBGCLMNO $.
    $( [3-Jan-2013] $)

