$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordering on reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Alias for ~ axlttrn , for naming consistency with ~ lttri . $)
  lttr $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A < B /\ B < C ) -> A < C ) ) $=
    ( axlttrn ) ABCD $.
    $( [10-Mar-2008] $)

  $( The product of two positive numbers is positive. $)
  mulgt0 $p |- ( ( ( A e. RR /\ 0 < A ) /\
                ( B e. RR /\ 0 < B ) ) -> 0 < ( A x. B ) ) $=
    ( cr wcel cc0 clt wbr cmul co wa axmulgt0 imp an4s ) ACDZBCDZEAFGZEBFGZEABH
    IFGZNOJPQJRABKLM $.
    $( [12-Jun-2009] $) $( [10-Mar-2008] $)

  $( 'Less than or equal to' expressed in terms of 'less than'. $)
  lenlt $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B <-> -. B < A ) ) $=
    ( cr wcel cxr cle wbr clt wn wb rexr xrlenlt syl2an ) ACDAEDBEDABFGBAHGIJBC
    DAKBKABLM $.
    $( [15-Oct-2005] $) $( [13-May-1999] $)

  $( 'Less than' expressed in terms of 'less than or equal to'. $)
  ltnle $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> -. B <_ A ) ) $=
    ( cr wcel wa cle wbr clt wn wb lenlt ancoms con2bid ) ACDZBCDZEBAFGZABHGZON
    PQIJBAKLM $.
    $( [11-Jul-2005] $)

  ${
    $d x y z $.
    $( 'Less than' is a strict ordering. $)
    ltso $p |- < Or RR $=
      ( vx vy vz cr clt cv wcel w3a wceq wo wn wb wa wi axlttri 3adant3 axlttrn
      wbr jca so ) ABCDEAFZDGZBFZDGZCFZDGZHUAUCERZUAUCIUCUAERJKLZUGUCUEERMUAUEE
      RNUBUDUHUFUAUCOPUAUCUEQST $.
      $( [19-Jan-1997] $)
  $}

  $( Consequence of trichotomy. $)
  lttri2 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A =/= B <-> ( A < B \/ B < A ) ) ) $=
    ( cr wcel wa clt wbr wo wceq wn wor wb ltso sotrieq mpan bicomd necon1abid
    ) ACDBCDEZABFGBAFGHZABRABIZSJZCFKRTUALMCABFNOPQ $.
    $( [22-Aug-2007] $) $( [9-Oct-1999] $)

  $( Trichotomy law for 'less than'. $)
  lttri3 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( -. A < B /\ -. B < A ) ) ) $=
    ( cr clt wor wcel wa wceq wbr wn wb ltso sotrieq2 mpan ) CDEACFBCFGABHABDIJ
    BADIJGKLCABDMN $.
    $( [5-May-1999] $)

  $( Trichotomy law for 'less than'.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  lttri4 $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B \/ A = B \/ B < A ) ) $=
    ( cr clt wor wcel wa wbr wceq w3o ltso solin mpan ) CDEACFBCFGABDHABIBADHJK
    CABDLM $.
    $( [19-Nov-2011] $) $( [20-Sep-2007] $)

  $( 'Less than' implies not equal. $)
  ltne $p |- ( ( A e. RR /\ B e. RR /\ A < B ) -> B =/= A ) $=
    ( cr wcel clt wbr w3a wne wa wo orc lttri2 syl5ibr 3impia necomd ) ACDZBCDZ
    ABEFZGABPQRABHZRSPQIRBAEFZJRTKABLMNO $.
    $( [6-Aug-2007] $) $( [9-Oct-1999] $)

  $( Trichotomy law. $)
  letri3 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( A <_ B /\ B <_ A ) ) ) $=
    ( cr wcel wa wceq clt wbr wn cle lttri3 ancom syl6bbr ancoms anbi12d bitr4d
    lenlt wb ) ACDZBCDZEZABFZBAGHIZABGHIZEZABJHZBAJHZEUAUBUDUCEUEABKUCUDLMUAUFU
    CUGUDABQTSUGUDRBAQNOP $.
    $( [14-May-1999] $)

  $( 'Less than or equal to' expressed in terms of 'less than' or 'equals'. $)
  leloe $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A <_ B <-> ( A < B \/ A = B ) ) ) $=
    ( cr wcel wa cle wbr clt wn wceq wo lenlt eqcom orbi1i orcom axlttri ancoms
    bitri wb con2bid syl5rbbr bitrd ) ACDZBCDZEZABFGBAHGZIZABHGZABJZKZABLUJBAJZ
    UHKZUEUGULUIUHKUJUKUIUHBAMNUIUHORUEUFULUDUCUFULISBAPQTUAUB $.
    $( [13-May-1999] $)

  $( Equality in terms of 'less than or equal to', 'less than'. $)
  eqlelt $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A = B <-> ( A <_ B /\ -. A < B ) ) ) $=
    ( cr wcel wa wceq cle wbr clt wn letri3 wb lenlt ancoms anbi2d bitrd ) ACDZ
    BCDZEZABFABGHZBAGHZETABIHJZEABKSUAUBTRQUAUBLBAMNOP $.
    $( [7-Apr-2001] $)

  $( 'Less than' implies 'less than or equal to'. $)
  ltle $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B -> A <_ B ) ) $=
    ( clt wbr cle cr wcel wa wceq wo orc leloe syl5ibr ) ABCDZABEDAFGBFGHNABIZJ
    NOKABLM $.
    $( [25-Aug-1999] $)

  $( 'Less than or equal to' implies 'less than' is not 'equals'. $)
  leltne $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) ->
                ( A < B <-> B =/= A ) ) $=
    ( cr wcel cle wbr clt wne wb wa wceq wn wi lttri3 simpl syl6bi adantr leloe
    wo biimpa ord impbid necon2abid necom syl6bbr 3impa ) ACDZBCDZABEFZABGFZBAH
    ZIUGUHJZUIJZUJABHUKUMUJABUMABKZUJLZULUNUOMUIULUNUOBAGFLZJUOABNUOUPOPQUMUJUN
    ULUIUJUNSABRTUAUBUCBAUDUEUF $.
    $( [18-Jul-2007] $) $( [27-Jul-1999] $)

  $( 'Less than' expressed in terms of 'less than or equal to'. $)
  ltlen $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B <-> ( A <_ B /\ B =/= A ) ) ) $=
    ( cr wcel wa clt wbr cle wne ltle ltne 3expia jcad wceq wo wi leloe ax-1 wn
    df-ne pm2.24 eqcoms syl5bi jaoi syl6bi imp3a impbid ) ACDZBCDZEZABFGZABHGZB
    AIZEUJUKULUMABJUHUIUKUMABKLMUJULUMUKUJULUKABNZOUMUKPZABQUKUOUNUKUMRUMBANZSZ
    UNUKBATUQUKPBAUPUKUAUBUCUDUEUFUG $.
    $( [7-Aug-2007] $) $( [27-Oct-1999] $)

  $( Transitive law. $)
  lelttr $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <_ B /\ B < C ) -> A < C ) ) $=
    ( cr wcel w3a cle wbr clt wceq wo wi wb leloe 3adant3 axlttrn exp3a biimprd
    breq1 a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACIHZUGUHABIHZABJZKZU
    IUJLZUDUEUHUMMUFABNOUGUKUNULUGUKUIUJABCPQULUNLUGULUJUIABCISRTUAUBUC $.
    $( [23-May-1999] $)

  $( Transitive law. $)
  ltletr $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A < B /\ B <_ C ) -> A < C ) ) $=
    ( cr wcel w3a clt wbr wceq wo wi wb leloe 3adant1 axlttrn exp3a com23 breq2
    cle biimpd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCSHZACGHZUHUJUIUKUHU
    JBCGHZBCIZJZUIUKKZUFUGUJUNLUEBCMNUHULUOUMUHUIULUKUHUIULUKABCOPQUMUOKUHUMUIU
    KBCAGRTUAUBUCQUD $.
    $( [25-Aug-1999] $)

  $( Transitive law. $)
  letr $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cr wcel w3a cle wbr wa clt wceq wo wb leloe 3adant1 adantr lelttr wi ltle
    3adant2 syld expdimp breq2 biimpcd adantl jaod sylbid expimpd ) ADEZBDEZCDE
    ZFZABGHZBCGHZACGHZULUMIZUNBCJHZBCKZLZUOULUNUSMZUMUJUKUTUIBCNOPUPUQUOURULUMU
    QUOULUMUQIACJHZUOABCQUIUKVAUORUJACSTUAUBUMURUORULURUMUOBCAGUCUDUEUFUGUH $.
    $( [12-Nov-1999] $)

  ${
    letrd.1 $e |- ( ph -> A e. RR ) $.
    letrd.2 $e |- ( ph -> B e. RR ) $.
    letrd.3 $e |- ( ph -> C e. RR ) $.
    ${
      letrd.4 $e |- ( ph -> A <_ B ) $.
      letrd.5 $e |- ( ph -> B <_ C ) $.
      $( Transitive law deduction for 'less than or equal to'. $)
      letrd $p |- ( ph -> A <_ C ) $=
        ( cle wbr cr wcel wa wi letr syl3anc mp2and ) ABCJKZCDJKZBDJKZHIABLMCLM
        DLMSTNUAOEFGBCDPQR $.
        $( [20-May-2005] $)
    $}

    ${
      lelttrd.4 $e |- ( ph -> A <_ B ) $.
      lelttrd.5 $e |- ( ph -> B < C ) $.
      $( Transitive law deduction for 'less than or equal to', 'less than'. $)
      lelttrd $p |- ( ph -> A < C ) $=
        ( cle wbr clt cr wcel wa wi lelttr syl3anc mp2and ) ABCJKZCDLKZBDLKZHIA
        BMNCMNDMNTUAOUBPEFGBCDQRS $.
        $( [8-Jan-2006] $)
    $}

    ${
      ltletrd.4 $e |- ( ph -> A < B ) $.
      ltletrd.5 $e |- ( ph -> B <_ C ) $.
      $( Transitive law deduction for 'less than', 'less than or equal to'. $)
      ltletrd $p |- ( ph -> A < C ) $=
        ( clt wbr cle cr wcel wa wi ltletr syl3anc mp2and ) ABCJKZCDLKZBDJKZHIA
        BMNCMNDMNTUAOUBPEFGBCDQRS $.
        $( [9-Jan-2006] $)
    $}

    ${
      lttrd.4 $e |- ( ph -> A < B ) $.
      lttrd.5 $e |- ( ph -> B < C ) $.
      $( Transitive law deduction for 'less than'. $)
      lttrd $p |- ( ph -> A < C ) $=
        ( clt wbr cr wcel wa wi axlttrn syl3anc mp2and ) ABCJKZCDJKZBDJKZHIABLM
        CLMDLMSTNUAOEFGBCDPQR $.
        $( [9-Jan-2006] $)
    $}
  $}

  $( 'Less than' is irreflexive. $)
  ltnr $p |- ( A e. RR -> -. A < A ) $=
    ( cr clt wor wcel wbr wn ltso sonr mpan ) BCDABEAACFGHBACIJ $.
    $( [18-Aug-1999] $)

  $( 'Less than or equal to' is reflexive. $)
  leid $p |- ( A e. RR -> A <_ A ) $=
    ( cr wcel cle wbr wa clt wceq wo eqid olci leloe mpbiri anidms ) ABCZAADEZO
    OFPAAGEZAAHZIRQAJKAALMN $.
    $( [18-Aug-1999] $)

  $( 'Less than' is not symmetric. $)
  ltnsym $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B -> -. B < A ) ) $=
    ( cr wcel wa clt wbr wceq wo wn axlttri pm2.46 syl6bi ) ACDBCDEABFGABHZBAFG
    ZIJOJABKNOLM $.
    $( [8-Jan-2002] $)

  $( 'Less than' is antisymmetric and irreflexive.  (The proof was shortened by
     Andrew Salmon, 19-Nov-2011.) $)
  ltnsym2 $p |- ( ( A e. RR /\ B e. RR ) -> -. ( A < B /\ B < A ) ) $=
    ( cr clt wor wcel wa wbr wn ltso so2nr mpan ) CDEACFBCFGABDHBADHGIJCABDKL
    $.
    $( [19-Nov-2011] $) $( [13-Aug-2005] $)

  ${
    pm2.61ltle.1 $e |- ( ( ph /\ A < B ) -> ps ) $.
    pm2.61ltle.2 $e |- ( ( ph /\ B <_ A ) -> ps ) $.
    pm2.61ltle.3 $e |- ( ph -> A e. RR ) $.
    pm2.61ltle.4 $e |- ( ph -> B e. RR ) $.
    $( Ordering elimination by cases. $)
    pm2.61ltlei $p |- ( ph -> ps ) $=
      ( clt wbr wn cle cr wcel wb lenlt syl2anc biimpar syldan pm2.61dan ) ACDI
      JZBEAUAKZDCLJZBAUCUBADMNCMNUCUBOHGDCPQRFST $.
      $( [1-Jul-2007] $)
  $}

  $( Equality implies 'less than or equal to'. $)
  eqle $p |- ( ( A e. RR /\ A = B ) -> A <_ B ) $=
    ( cr wcel cle wbr wceq leid breq2 biimpac sylan ) ACDAAEFZABGZABEFZAHMLNABA
    EIJK $.
    $( [4-Apr-2005] $)

  ${
    lt.1 $e |- A e. RR $.

    ${
      lt.2 $e |- B e. RR $.
      $( Consequence of trichotomy. $)
      lttri2i $p |- ( A =/= B <-> ( A < B \/ B < A ) ) $=
        ( cr wcel wne clt wbr wo wb lttri2 mp2an ) AEFBEFABGABHIBAHIJKCDABLM $.
        $( [22-Aug-2007] $) $( [19-Jan-1997] $)

      $( Consequence of trichotomy. $)
      lttri3i $p |- ( A = B <-> ( -. A < B /\ -. B < A ) ) $=
        ( cr wcel wceq clt wbr wn wa wb lttri3 mp2an ) AEFBEFABGABHIJBAHIJKLCDA
        BMN $.
        $( [14-May-1999] $)

      $( Consequence of trichotomy. $)
      letri3i $p |- ( A = B <-> ( A <_ B /\ B <_ A ) ) $=
        ( cr wcel wceq cle wbr wa wb letri3 mp2an ) AEFBEFABGABHIBAHIJKCDABLM
        $.
        $( [14-May-1999] $)

      $( 'Less than or equal to' in terms of 'less than'. $)
      leloei $p |- ( A <_ B <-> ( A < B \/ A = B ) ) $=
        ( cr wcel cle wbr clt wceq wo wb leloe mp2an ) AEFBEFABGHABIHABJKLCDABM
        N $.
        $( [14-May-1999] $)

      $( 'Less than' expressed in terms of 'less than or equal to'. $)
      ltleni $p |- ( A < B <-> ( A <_ B /\ B =/= A ) ) $=
        ( cr wcel clt wbr cle wne wa wb ltlen mp2an ) AEFBEFABGHABIHBAJKLCDABMN
        $.
        $( [8-Aug-2007] $) $( [27-Oct-1999] $)

      $( 'Less than' is not symmetric. $)
      ltnsymi $p |- ( A < B -> -. B < A ) $=
        ( cr wcel clt wbr wn wi ltnsym mp2an ) AEFBEFABGHBAGHIJCDABKL $.
        $( [6-May-1999] $)

      $( 'Less than or equal to' in terms of 'less than'. $)
      lenlti $p |- ( A <_ B <-> -. B < A ) $=
        ( cr wcel cle wbr clt wn wb lenlt mp2an ) AEFBEFABGHBAIHJKCDABLM $.
        $( [24-May-1999] $)

      $( 'Less than' in terms of 'less than or equal to'. $)
      ltnlei $p |- ( A < B <-> -. B <_ A ) $=
        ( cle wbr clt lenlti con2bii ) BAEFABGFBADCHI $.
        $( [11-Jul-2005] $)

      $( 'Less than' implies 'less than or equal to'. $)
      ltlei $p |- ( A < B -> A <_ B ) $=
        ( cr wcel clt wbr cle wi ltle mp2an ) AEFBEFABGHABIHJCDABKL $.
        $( [14-May-1999] $)

      ${
        ltlei.1 $e |- A < B $.
        $( 'Less than' implies 'less than or equal to' (inference). $)
        ltleii $p |- A <_ B $=
          ( clt wbr cle ltlei ax-mp ) ABFGABHGEABCDIJ $.
          $( [22-Aug-1999] $)
      $}

      $( Equality implies 'less than or equal to'. $)
      eqlei $p |- ( A = B -> A <_ B ) $=
        ( wceq cle wbr letri3i simplbi ) ABEABFGBAFGABCDHI $.
        $( [23-May-1999] $)

      $( 'Less than' implies not equal. $)
      ltnei $p |- ( A < B -> B =/= A ) $=
        ( cr wcel clt wbr wne ltne mp3an12 ) AEFBEFABGHBAICDABJK $.
        $( [14-Aug-2007] $) $( [28-Jul-1999] $)

      $( Trichotomy law for 'less than or equal to'. $)
      letrii $p |- ( A <_ B \/ B <_ A ) $=
        ( cle wbr wn clt ltnlei ltlei sylbir orri ) ABEFZBAEFZMGBAHFNBADCIBADCJ
        KL $.
        $( [2-Aug-1999] $)

      ${
        lt.3 $e |- C e. RR $.
        $( 'Less than' is transitive.  Theorem I.17 of [Apostol] p. 20. $)
        lttri $p |- ( ( A < B /\ B < C ) -> A < C ) $=
          ( cr wcel clt wbr wa wi axlttrn mp3an ) AGHBGHCGHABIJBCIJKACIJLDEFABC
          MN $.
          $( [14-May-1999] $)

        $( 'Less than or equal to', 'less than' transitive law. $)
        lelttri $p |- ( ( A <_ B /\ B < C ) -> A < C ) $=
          ( cr wcel cle wbr clt wa wi lelttr mp3an ) AGHBGHCGHABIJBCKJLACKJMDEF
          ABCNO $.
          $( [14-May-1999] $)

        $( 'Less than', 'less than or equal to' transitive law. $)
        ltletri $p |- ( ( A < B /\ B <_ C ) -> A < C ) $=
          ( cr wcel clt wbr cle wa wi ltletr mp3an ) AGHBGHCGHABIJBCKJLACIJMDEF
          ABCNO $.
          $( [14-May-1999] $)

        $( 'Less than or equal to' is transitive. $)
        letri $p |- ( ( A <_ B /\ B <_ C ) -> A <_ C ) $=
          ( cr wcel cle wbr wa wi letr mp3an ) AGHBGHCGHABIJBCIJKACIJLDEFABCMN
          $.
          $( [14-May-1999] $)

        $( Extended trichotomy law for 'less than or equal to'. $)
        le2tri3i $p |- ( ( A <_ B /\ B <_ C /\ C <_ A ) <->
                        ( A = B /\ B = C /\ C = A ) ) $=
          ( cle wbr w3a wceq wa letri letri3i biimpri sylan2 3impb 3comr eqcomd
          sylan eqlei 3impa 3jca 3anim123i impbii ) ABGHZBCGHZCAGHZIZABJZBCJZCA
          JZIUHUIUJUKUEUFUGUIUFUGKUEBAGHZUIBCAEFDLUIUEULKABDEMNOPUFUGUEUJUFUGUE
          UJUGUEKUFCBGHZUJCABFDELUJUFUMKBCEFMNOPQUEUFUGUKUEUFKACGHZUGUKABCDEFLU
          NUGKZACACJUOACDFMNRSUAUBUIUEUJUFUKUGABDETBCEFTCAFDTUCUD $.
          $( [14-Aug-2000] $)

        $( Addition to both sides of 'less than'.  (Proof shortened by Paul
           Chapman, 27-Jan-2008.) $)
        ltadd2i $p |- ( A < B <-> ( C + A ) < ( C + B ) ) $=
          ( clt wbr caddc co cr wcel wi axltadd mp3an cle wn wceq wo leloei
          oveq2 orim12i readdcli 3imtr4i lenlti 3imtr3i con4i impbii ) ABGHZCAI
          JZCBIJZGHZAKLZBKLZCKLZUIULMDEFABCNOUIULBAPHZUKUJPHZUIQULQBAGHZBARZSUK
          UJGHZUKUJRZSUPUQURUTUSVAUNUMUOURUTMEDFBACNOBACIUAUBBAEDTUKUJCBFEUCZCA
          FDUCZTUDBAEDUEUKUJVBVCUEUFUGUH $.
          $( [28-Jan-2008] $) $( [21-Jan-1997] $)


      $}

      $( The product of two positive numbers is positive. $)
      mulgt0i $p |- ( ( 0 < A /\ 0 < B ) -> 0 < ( A x. B ) ) $=
        ( cr wcel cc0 clt wbr wa cmul co wi axmulgt0 mp2an ) AEFBEFGAHIGBHIJGAB
        KLHIMCDABNO $.
        $( [16-May-1999] $)

      ${
        mulgt0i.3 $e |- 0 < A $.
        mulgt0i.4 $e |- 0 < B $.
        $( The product of two positive numbers is positive. $)
        mulgt0ii $p |- 0 < ( A x. B ) $=
          ( cc0 clt wbr cmul co mulgt0i mp2an ) GAHIGBHIGABJKHIEFABCDLM $.
          $( [18-May-1999] $)
      $}
    $}

    $( 'Less than' is irreflexive. $)
    ltnri $p |- -. A < A $=
      ( cr wcel clt wbr wn ltnr ax-mp ) ACDAAEFGBAHI $.
      $( [18-Aug-1999] $)
  $}

  ${
    ltneii.1 $e |- A e. RR $.
    ltneii.2 $e |- B e. RR $.
    ltneii.3 $e |- A < B $.
    $( 'Less than' implies not equal. $)
    ltneii $p |- B =/= A $=
      ( clt wbr wne ltnei ax-mp ) ABFGBAHEABCDIJ $.
      $( [30-Sep-2013] $)

    $( Obsolete version of ~ ltneii as of 30-Sep-2013. $)
    ltneiiOLD $p |- A =/= B $=
      ( ltneii necomi ) BAABCDEFG $.
      $( [15-Sep-2013] $)
  $}

  $( A nonzero nonnegative number is positive. $)
  ne0gt0 $p |- ( ( A e. RR /\ 0 <_ A ) -> ( A =/= 0 <-> 0 < A ) ) $=
    ( cr wcel cc0 cle wbr wa wne clt wo wb 0re lttri2 mpan2 adantr lenlt biimpa
    wn mpan biorf syl bitr4d ) ABCZDAEFZGZADHZADIFZDAIFZJZUHUCUFUIKZUDUCDBCZUJL
    ADMNOUEUGRZUHUIKUCUDULUKUCUDULKLDAPSQUGUHTUAUB $.
    $( [20-Nov-2007] $)

  $( Trichotomy law.  (The proof was shortened by Andrew Salmon,
     19-Nov-2011.) $)
  letric $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B \/ B <_ A ) ) $=
    ( cr wcel cle wbr wo wa wn clt ltnle ltle sylbird orrd ancoms ) BCDZACDZABE
    FZBAEFZGPQHZRSTRIBAJFSBAKBALMNO $.
    $( [19-Nov-2011] $) $( [18-Aug-1999] $)

  ${
    lecase.1 $e |- ( ph -> A e. RR ) $.
    lecase.2 $e |- ( ph -> B e. RR ) $.
    lecase.3 $e |- ( ( ph /\ A <_ B ) -> ps ) $.
    lecase.4 $e |- ( ( ph /\ B <_ A ) -> ps ) $.
    $( Ordering elimination by cases. $)
    lecasei $p |- ( ph -> ps ) $=
      ( cle wbr wo cr wcel letric syl2anc jaodan mpdan ) ACDIJZDCIJZKZBACLMDLMT
      EFCDNOARBSGHPQ $.
      $( [6-Jul-2007] $)
  $}

  $( Trichotomy law. $)
  lelttric $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B \/ B < A ) ) $=
    ( cr wcel wa cle wbr clt wo wn pm2.1 lenlt orbi1d mpbiri ) ACDBCDEZABFGZBAH
    GZIQJZQIQKOPRQABLMN $.
    $( [4-Apr-2005] $)

