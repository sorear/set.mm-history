$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intermediate value theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d c x y z B $.  $d c x y z D $.  $d c x y z F $.  $d c x y z ph $.
    $d c x y A $.  $d x y z C $.  $d x y z S $.  $d c x y z U $.
    ivth.1 $e |- ( ph -> A e. RR ) $.
    ivth.2 $e |- ( ph -> B e. RR ) $.
    ivth.3 $e |- ( ph -> U e. RR ) $.
    ivth.4 $e |- ( ph -> A < B ) $.
    ivth.5 $e |- ( ph -> ( A [,] B ) C_ D ) $.
    ivth.6 $e |- ( ph -> D C_ CC ) $.
    ivth.7 $e |- ( ph -> F e. ( D -cn-> CC ) ) $.
    ivth.8 $e |- ( ( ph /\ x e. ( A [,] B ) ) -> ( F ` x ) e. RR ) $.
    ${
      ivth.9 $e |- ( ph -> ( ( F ` A ) < U /\ U < ( F ` B ) ) ) $.
      ${
        ivth.10 $e |- S = { x e. ( A [,] B ) | ( F ` x ) <_ U } $.
        ivth.11 $e |- C = sup ( S , RR , < ) $.
        $( Lemma for ~ ivth , the intermediate value theorem, increasing case
           with supremum solution. $)
        ivthlem $p |- ( ph -> ( C e. ( A (,) B ) /\ ( F ` C ) = U ) ) $=
          ( vz vy cioo co wcel cfv wceq clt wbr cxr w3a rexr syl elioo2 syl2anc
          cr wb csup wss c0 wne cv wral wrex cicc ssrab2 eqsstri iccssre syl5ss
          cle crab wi ltle mpd lbicc2 syl3anc fveq2 eleq1d rcla4v breq1d elrab2
          sylc sseli syl5 breq2 rcla4ev wn wa elicc2 suprub syl6breqr mpbir3and
          cmin cabs crp cc adantr a1i sseldd difrp biimpa cncfi syl32anc ssralv
          ad2antrr adantl rpre readdcl mpan2d imp breq2d notbid necon2ad jctild
          caddc ltlen sylibrd mpbid eqbrtrd r19.29 pm3.45 simprr simprl simplll
          syl12anc resubcl absdiflt recnd ad2antrl ex lenlt syld sylbid pm2.21d
          expr com23 imp3a rexlimdva pm2.01d ad2antll simpld ralrimiva sylanbrc
          ne0i iccleub 3expia ralrimiv 3jca syl5eqel suprleub syl22anc syl5eqbr
          ralbidv suprcl lttri3 ccncf ssid cdiv cif rphalfcl ifcl simprd ubicc2
          c2 lttr ltnr syl5ibrcom ltaddrp ifboth min1 abssubge0 rphalflt ltadd2
          letrd min2 lelttrd ltsubadd2 mpbird oveq1 fveq2d anbi12d baib 3imtr4d
          pncan3 sylibd adantld mt2d ltsubrp sylancom suprlub abssuble0 ltsub23
          syl6breq jca32 reximdv2 nncan simprbi adantrd mpbir2and syl5ibcom jca
          breqtrrd ) AECDUCUDUEZEIUFZHUGZAUXCEUPUEZCEUHUIZEDUHUIZACUJUEZDUJUEZU
          XCUXFUXGUXHUKUQACUPUEZUXIJCULUMZADUPUEZUXJKDULUMZCDEUNUOAEGUPUHURZUPT
          AGUPUSZGUTVAZUAVBZBVBZVJUIZUAGVCZBUPVDZUKZUXOUPUEAUXPUXQUYBAGCDVEUDZU
          PGUXSIUFZHVJUIZBUYDVKUYDSUYFBUYDVFVGZAUXKUXMUYDUPUSZJKCDVHUOZVIZACGUE
          ZUXQACUYDUEZCIUFZHVJUIZUYKAUXKUXMCDVJUIZUYLJKACDUHUIZUYOMAUXKUXMUYPUY
          OVLJKCDVMUOVNZCDVOVPZAUYMHUHUIZUYNAUYSHDIUFZUHUIZRUUAZAUYMUPUEZHUPUEZ
          UYSUYNVLAUYLUYEUPUEZBUYDVCZVUCUYRAVUEBUYDQUUBZVUEVUCBCUYDUXSCUGZUYEUY
          MUPUXSCIVQZVRVSWBLUYMHVMUOVNUYFUYNBCUYDGVUHUYEUYMHVJVUIVTSWAUUCZGCUUD
          UMZAUXMUXRDVJUIZUAGVCZUYBKAVULUAGUXRGUEUXRUYDUEZAVULGUYDUXRUYGWCAUXIU
          XJVUNVULVLUXLUXNUXIUXJVUNVULCDUXRUUEUUFUOWDUUGZUYAVUMBDUPUXSDUGZUXTVU
          LUAGUXSDUXRVJWEUUMWFUOUUHZBUAGUUNUMUUIZAUYMUXDUHUIZUXGAUYMHUXDUHVUBAU
          XEUXDHUHUIZWGZHUXDUHUIZWGZAUXDUPUEZVUDUXEVVAVVCWHUQAEUYDUEZVUFVVDAVVE
          UXFCEVJUIZEDVJUIZAUXKUXMVVEUXFVVFVVGUKUQJKCDEWIUOVURACUXOEVJAUYCUYKCU
          XOVJUIVUQVUJBUAGCWJUOTWKZAEUXODVJTAUXPUXQUXMVUMUXODVJUIUYJVUKKVUOUAGD
          UUJUUKUULZWLZVUGVUEVVDBEUYDUXSEUGUYEUXDUPUXSEIVQVRVSWBZLUXDHUUOUOAVUT
          AVUTVVAAVUTWHZUBVBZEWMUDZWNUFZUXRUHUIZVVMIUFZUXDWMUDWNUFZHUXDWMUDZUHU
          IZVLZUBFVCZUAWOVDZVVAVVLFWPUSZWPWPUSZIFWPUUPUDUEZEFUEZVVSWOUEZVWCAVWD
          VUTOWQVWEVVLWPUUQZWRAVWFVUTPWQAVWGVUTAUYDFENVVJWSZWQAVUTVWHAVVDVUDVUT
          VWHUQVVKLUXDHWTUOXAUAUBFWPEVVSIXBXCVVLVWBVVAUAWOVVLUXRWOUEZWHZVWBVWAU
          BUYDVCZVVAAVWBVWMVLZVUTVWKAUYDFUSZVWNNVWAUBUYDFXDUMXEVWLVWMVVPEVVMUHU
          IZWHZUBUYDVDZVVAVWLDEUXRUVDUURUDZXOUDZVJUIZDVWTUUSZUYDUEZVXBEWMUDZWNU
          FZUXRUHUIZEVXBUHUIZVWRVWLVXCVXBUPUEZCVXBVJUIZVXBDVJUIZAVXCVXHVXIVXJUK
          UQZVUTVWKAUXKUXMVXKJKCDVXBWIUOXEVWLUXMVWTUPUEZVXHAUXMVUTVWKKXEZVWLUXF
          VWSUPUEZVXLAUXFVUTVWKVURXEZVWLVWSWOUEZVXNVWKVXPVVLUXRUUTXFZVWSXGUMZEV
          WSXHUOZVXADVWTUPUVAUOZVWLCEVXBAUXKVUTVWKJXEVXOVXTAVVFVUTVWKVVHXEVWLVX
          GEVXBVJUIZVWLUXHEVWTUHUIZVXGVWLUXDUYTUHUIZUXHVVLVYCVWKAVUTVYCAVUTVUAV
          YCAUYSVUARUVBZAVVDVUDUYTUPUEZVUTVUAWHVYCVLVVKLADUYDUEZVUFVYEAUXKUXMUY
          OVYFJKUYQCDUVCVPVUGVUEVYEBDUYDVUPUYEUYTUPUXSDIVQVRVSWBUXDHUYTUVEVPXIX
          JWQAVYCUXHVLVUTVWKAVYCVVGDEVAZWHZUXHAVYCVYGVVGAVYCDEAVYCWGDEUGZUXDUXD
          UHUIZWGZAVVDVYKVVKUXDUVFUMZVYIVYCVYJVYIUYTUXDUXDUHDEIVQXKXLUVGXMVVIXN
          AUXFUXMUXHVYHUQVURKEDXPUOXQZXEVNVWLUXFVXPVYBVXOVXQEVWSUVHUOVXAUXHVYBV
          XGDVWTDVXBEUHWEVWTVXBEUHWEUVIUOZVWLUXFVXHVXGVYAVLVXOVXTEVXBVMUOVNZUVN
          VWLUXMVXLVXJVXMVXSDVWTUVJUOWLVWLVXEVXDUXRUHVWLUXFVXHVYAVXEVXDUGVXOVXT
          VYOEVXBUVKVPVWLVXDUXRUHUIZVXBEUXRXOUDZUHUIZVWLVXBVWTVYQVXTVXSVWLUXFUX
          RUPUEZVYQUPUEVXOVWKVYSVVLUXRXGZXFZEUXRXHUOVWLUXMVXLVXBVWTVJUIVXMVXSDV
          WTUVOUOVWLVWSUXRUHUIZVWTVYQUHUIZVWKWUBVVLUXRUVLXFVWLVXNVYSUXFWUBWUCUQ
          VXRWUAVXOVWSUXREUVMVPXRUVPVWLVXHUXFVYSVYPVYRUQVXTVXOWUAVXBEUXRUVQVPUV
          RXSVYNVWQVXFVXGWHUBVXBUYDVVMVXBUGZVVPVXFVWPVXGWUDVVOVXEUXRUHWUDVVNVXD
          WNVVMVXBEWMUVSUVTVTVVMVXBEUHWEUWAWFYEVWMVWRWHVWAVWQWHZUBUYDVDVWLVVAVW
          AVWQUBUYDXTVWLWUEVVAUBUYDWUEVVTVWPWHZVWLVVMUYDUEZWHZVVAVWAVWQWUFVVPVV
          TVWPYAXJWUHVVTVWPVVAWUHVWPVVTVVAVWLWUGVWPVVTVVAVLVWLWUGVWPWHZWHZVVTVV
          AWUJVVTVWPVWLWUGVWPYBWUJVVTUXDVVSWMUDVVQUHUIZVVQUXDVVSXOUDZUHUIZWHZVW
          PWGZWUJVVQUPUEZVVDVVSUPUEZVVTWUNUQWUJWUGVUFWUPVWLWUGVWPYCZWUJAVUFAVUT
          VWKWUIYDZVUGUMVUEWUPBVVMUYDUXSVVMUGZUYEVVQUPUXSVVMIVQZVRVSZWBZWUJAVVD
          WUSVVKUMZWUJVUDVVDWUQWUJAVUDWUSLUMZWVDHUXDYFUOVVQUXDVVSYGVPWUJWUMWUOW
          UKWUJWUMVVMGUEZWUOWUJVVQHUHUIZVVQHVJUIZWUMWVFWUJWUPVUDWVGWVHVLWVCWVEV
          VQHVMUOWUJWULHVVQUHWUJUXDWPUEZHWPUEZWULHUGWUJUXDWVDYHWUJHWVEYHUXDHUWD
          UOXKWUGWVFWVHUQVWLVWPWVFWUGWVHUYFWVHBVVMUYDGWUTUYEVVQHVJWVAVTSWAZUWBY
          IUWCWUJWVFVVMEVJUIZWUOWUJAWVFWVLVLZWUSAUYCWVMVUQUYCWVFWVLUYCWVFWHVVMU
          XOEVJBUAGVVMWJZTWKYJUMUMWUJVVMUPUEZUXFWVLWUOUQWUJUYDUPVVMWUJAUYHWUSUY
          IUMWURWSWUJAUXFWUSVURUMVVMEYKUOUWEYLUWFYMUWGYNYOYPYQWDYRWDXIYLYRVNYJY
          SAVVBAVVBVVCAVVBWHZVVPVVRUXDHWMUDZUHUIZVLZUBFVCZUAWOVDZVVCWVPVWDVWEVW
          FVWGWVQWOUEZWWAAVWDVVBOWQVWEWVPVWIWRAVWFVVBPWQAVWGVVBVWJWQAVVBWWBAVUD
          VVDVVBWWBUQLVVKHUXDWTUOXAUAUBFWPEWVQIXBXCWVPWVTVVCUAWOWVPVWKWHZWVTWVS
          UBUYDVCZVVCAWVTWWDVLZVVBVWKAVWOWWENWVSUBUYDFXDUMXEWWCWWDVVPWVFWHZUBUY
          DVDZVVCWWCEUXRWMUDZVVMUHUIZUBGVDZWWGWWCUYCWWHUPUEZWWHUXOUHUIWWJAUYCVV
          BVWKVUQXEWWCUXFVYSWWKAUXFVVBVWKVURXEZVWKVYSWVPVYTXFZEUXRYFUOWWCWWHEUX
          OUHWVPVWKUXFWWHEUHUIWWLEUXRUWHUWITUWMBUAUBGWWHUWJYEWWCWWIWWFUBGUYDWWC
          WVFWWIWHZWUGWWFWHWWCWWNWHZWUGVVPWVFWVFWUGWWCWWIGUYDVVMUYGWCZYIZWWOVVO
          EVVMWMUDZUXRUHWWOWVOUXFWVLVVOWWRUGWWOUYDUPVVMWWOAUYHAVVBVWKWWNYDZUYIU
          MWWQWSZWWOAUXFWWSVURUMZWWOVVMUXOEVJWWOUYCWVFVVMUXOVJUIWWOAUYCWWSVUQUM
          WWCWVFWWIYCZWVNUOTWKVVMEUWKVPWWOWWIWWRUXRUHUIZWWCWVFWWIYBWWOUXFVYSWVO
          WWIWXCUQWXAWWCVYSWWNWWMWQWWTEUXRVVMUWLVPXRXSWXBUWNYJUWOVNWWDWWGWHWVSW
          WFWHZUBUYDVDWWCVVCWVSWWFUBUYDXTWWCWXDVVCUBUYDWXDWVRWVFWHZWWCWUGWHVVCW
          VSWWFWXEVVPWVRWVFYAXJWWCWXEVVCVLWUGWWCWVRWVFVVCWWCWVFWVRVVCWVPVWKWVFW
          VRVVCVLWVPVWKWVFWHZWHZWVRUXDWVQWMUDZVVQUHUIZVVQUXDWVQXOUDUHUIZWHZVVCW
          XGWUPVVDWVQUPUEZWVRWXKUQWXGWUGVUFWUPWVFWUGWVPVWKWWPYTAVUFVVBWXFVUGXEW
          VBWBZAVVDVVBWXFVVKXEZWXGVVDVUDWXLWXNAVUDVVBWXFLXEZUXDHYFUOVVQUXDWVQYG
          VPWXGWXIVVCWXJWXGWXIHVVQUHUIZVVCWXGWXHHVVQUHWXGWVIWVJWXHHUGWXGUXDWXNY
          HWXGHWXOYHUXDHUWPUOVTWXGWXPVVCWXGWVHWXPWGZWVFWVHWVPVWKWVFWUGWVHWVKUWQ
          YTWXGWUPVUDWVHWXQUQWXMWXOVVQHYKUOXRYNYMUWRYMYOYPYQWQWDYRWDXIYLYRVNYJY
          SUWSZUXBAVUSVVFECVAZWHZUXGAVUSWXSVVFAVUSECAVYKECUGZVUSWGVYLWYAVYJVUSW
          YAUXDUYMUXDUHECIVQVTXLUWTXMVVHXNAUXKUXFUXGWXTUQJVURCEXPUOXQVNAVYCUXHA
          UXDHUYTUHWXRVYDXSVYMVNWLWXRUXA $.
          $( [30-Apr-2014] $)
      $}

      $( The intermediate value theorem, increasing case.  (Contributed by Paul
         Chapman, 22-Jan-2008.)  (The proof was shortened by Mario Carneiro,
         30-Apr-2014.) $)
      ivth $p |- ( ph -> E. c e. ( A (,) B ) ( F ` c ) = U ) $=
        ( vy cfv wceq cv cle wbr cicc co crab cr clt csup cioo wcel wrex breq1d
        wa fveq2 cbvrabv eqid ivthlem eqeq1d rcla4ev syl ) ARUAZGSZFUBUCZRCDUDU
        EZUFZUGUHUIZCDUJUEZUKVGGSZFTZUNHUAZGSZFTZHVHULABCDVGEVFFGIJKLMNOPQVDBUA
        ZGSZFUBUCRBVEVBVNTVCVOFUBVBVNGUOUMUPVGUQURVMVJHVGVHVKVGTVLVIFVKVGGUOUSU
        TVA $.
        $( [30-Apr-2014] $) $( [22-Jan-2008] $)
    $}

    ${
      ivth2.9 $e |- ( ph -> ( ( F ` B ) < U /\ U < ( F ` A ) ) ) $.
      $( The intermediate value theorem, decreasing case.  (Contributed by Paul
         Chapman, 22-Jan-2008.)  (Revised by Mario Carneiro, 30-Apr-2014.) $)
      ivth2 $p |- ( ph -> E. c e. ( A (,) B ) ( F ` c ) = U ) $=
        ( wceq cr wcel vy cv cfv cneg cmpt cioo co wrex renegcl syl cc wss eqid
        ccncf negfcncf syl2anc cicc wa sseld imp fveq2 negeqd negex eqeltrd clt
        fvmpt wbr cle wi ltle mpd lbicc2 syl3anc sseldd simprd ralrimiva eleq1d
        wb wral rcla4v sylc ltneg mpbid eqbrtrd simpld ubicc2 breqtrrd jca ivth
        ioossicc syl5ss eqeq1d ssid a1i cncff ffvelrn sylan syldan recnd adantr
        wf neg11 bitrd rexbidva ) AHUBZUAEUAUBZGUCZUDZUEZUCZFUDZRZHCDUFUGZUHXEG
        UCZFRZHXMUHABCDEXKXIHIJAFSTZXKSTKFUIUJLMNAEUKULZGEUKUNUGZTZXIXRTNOUAEGX
        IXIUMZUOUPABUBZCDUQUGZTZURZYAXIUCZYAGUCZUDZSYDYAETZYEYGRAYCYHAYBEYAMUSU
        TUAYAXHYGEXIXFYARXGYFXFYAGVAVBXTYFVCVFUJYDYFSTZYGSTPYFUIUJVDACXIUCZXKVE
        VGXKDXIUCZVEVGAYJCGUCZUDZXKVEACETYJYMRAYBECMACSTZDSTZCDVHVGZCYBTZIJACDV
        EVGZYPLAYNYOYRYPVIIJCDVJUPVKZCDVLVMZVNUACXHYMEXIXFCRXGYLXFCGVAVBXTYLVCV
        FUJAFYLVEVGZYMXKVEVGZADGUCZFVEVGZUUAQVOAXPYLSTZUUAUUBVRKAYQYIBYBVSZUUEY
        TAYIBYBPVPZYIUUEBCYBYACRYFYLSYACGVAVQVTWAFYLWBUPWCWDAXKUUCUDZYKVEAUUDXK
        UUHVEVGZAUUDUUAQWEAUUCSTZXPUUDUUIVRADYBTZUUFUUJAYNYOYPUUKIJYSCDWFVMZUUG
        YIUUJBDYBYADRYFUUCSYADGVAVQVTWAKUUCFWBUPWCADETYKUUHRAYBEDMUULVNUADXHUUH
        EXIXFDRXGUUCXFDGVAVBXTUUCVCVFUJWGWHWIAXLXOHXMAXEXMTZURZXLXNUDZXKRZXOUUN
        XJUUOXKUUNXEETZXJUUORAUUMUUQAXMEXEAXMYBECDWJMWKUSUTZUAXEXHUUOEXIXFXERXG
        XNXFXEGVAVBXTXNVCVFUJWLUUNXNUKTZFUKTZUUPXOVRAUUMUUQUUSUURAEUKGXAZUUQUUS
        AXQUKUKULZXSUVANUVBAUKWMWNOEUKGWOVMEUKXEGWPWQWRAUUTUUMAFKWSWTXNFXBUPXCX
        DWC $.
        $( [30-Apr-2014] $) $( [22-Jan-2008] $)
    $}

    ivthle2.9 $e |- ( ph -> ( ( F ` B ) <_ U /\ U <_ ( F ` A ) ) ) $.
    $( The intermediate value theorem with weak inequality, decreasing case.
       (Contributed by Mario Carneiro, 12-May-2014.) $)
    ivthle2 $p |- ( ph -> E. c e. ( A [,] B ) ( F ` c ) = U ) $=
      ( wceq cr wcel cfv clt wbr wo cv cicc co wrex simpld wb wral ltle syl2anc
      cle wi mpd ubicc2 syl3anc ralrimiva fveq2 eleq1d rcla4v sylc leloe simprd
      mpbid wa lbicc2 adantr cioo wss ioossicc ccncf adantlr simpr ivth2 ssrexv
      cc mpsyl anassrs eqcom eqeq2d syl5bb rcla4ev sylan jaodan mpdan eqeq1d )
      ADGUAZFUBUCZWIFRZUDZHUEZGUAZFRZHCDUFUGZUHZAWIFUNUCZWLAWRFCGUAZUNUCZQUIAWI
      STZFSTZWRWLUJADWPTZBUEZGUAZSTZBWPUKZXAACSTZDSTZCDUNUCZXCIJACDUBUCZXJLAXHX
      IXKXJUOIJCDULUMUPZCDUQURZAXFBWPPUSZXFXABDWPXDDRXEWISXDDGUTVAVBVCKWIFVDUMV
      FAWJWQWKAWJVGZFWSUBUCZFWSRZUDZWQAXRWJAWTXRAWRWTQVEAXBWSSTZWTXRUJKACWPTZXG
      XSAXHXIXJXTIJXLCDVHURZXNXFXSBCWPXDCRXEWSSXDCGUTVAVBVCFWSVDUMVFVIXOXPWQXQA
      WJXPWQCDVJUGZWPVKAWJXPVGZVGZWOHYBUHWQCDVLYDBCDEFGHAXHYCIVIAXIYCJVIAXBYCKV
      IAXKYCLVIAWPEVKYCMVIAEVRVKYCNVIAGEVRVMUGTYCOVIAXDWPTXFYCPVNAYCVOVPWOHYBWP
      VQVSVTAXQWQWJAXTXQWQYAWOXQHCWPWOFWNRWMCRZXQWNFWAYEWNWSFWMCGUTWBWCWDWEVNWF
      WGAXCWKWQXMWOWKHDWPWMDRWNWIFWMDGUTWHWDWEWFWG $.
      $( [30-Apr-2014] $) $( [22-Jan-2008] $)
  $}


