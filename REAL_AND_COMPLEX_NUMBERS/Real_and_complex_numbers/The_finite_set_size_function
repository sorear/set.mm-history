$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               The ` # ` (finite set size) function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c # $.

  $( Extend the definition of a class to include the size function. $)
  chash $a class # $.

  ${
    $d x y $.
    $( Define the ` # ` function, which gives the cardinality of a finite set
       as a member of ` NN0 ` . $)
    df-hash $a |- # = ( ( rec (
                   ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) o. card ) $.
  $}

  ${
    $d A x y $.
    hashgval.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    $( The value of the ` # ` function in terms of the mapping ` G ` from
       ` om ` to ` NN0 ` .  The proof avoids the use of ~ ax-ac .  (Contributed
       by Paul Chapman, 22-Jun-2011.)  (Revised by Mario Carneiro,
       12-Feb-2013.) $)
    hashgval $p |- ( A e. Fin -> ( G ` ( card ` A ) ) = ( # ` A ) ) $=
      ( vy cfn wcel chash cfv ccrd ccom cv cc0 com wceq cen wbr con0 wrex wfun
      cvv c1 caddc co cmpt crdg cres df-hash coeq1i eqtr4i fveq1i cdm cab breq2
      finnum rexbidv elabg mpbird cardf2 fdmi syl6eleqr wfn rdgfnon funres mp2b
      fnfun funeqi mpbir wf ffun ax-mp fvco mp3an12 syl syl5req ) BFGZBHIBCJKZI
      ZBJICIZBHVQHAUAALZUBUCUDUEZMUFZNUGZJKVQAUHCWCJDUIUJUKVPBJULZGZVRVSOZVPBEL
      ZVTPQZERSZAUMZWDVPBWJGWGBPQZERSZEBUOWIWLABFVTBOWHWKERVTBWGPUNUPUQURWJRJAE
      USZUTVACTZJTZWEWFWNWCTZWBRVBWBTWPMWAVCRWBVFNWBVDVECWCDVGVHWJRJVIWOWMWJRJV
      JVKBCJVLVMVNVO $.
      $( [13-Sep-2013] $) $( [22-Jun-2011] $)

    $( ` ``' G ` maps the size function's value to ` card ` .  (Contributed by
       Paul Chapman, 22-Jun-2011.) $)
    hashginv $p |- ( A e. Fin -> ( `' G ` ( # ` A ) ) = ( card ` A ) ) $=
      ( cfn wcel ccrd cfv chash wceq ccnv hashgval com wi ficardom cn0 hashgf1o
      wf1o f1ocnvfv mpan syl mpd ) BEFZBGHZCHBIHZJZUECKHUDJZABCDLUCUDMFZUFUGNZB
      OMPCRUHUIACDQMPUDUECSTUAUB $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  ${
    $d x y z $.
    $( The size function maps all finite sets to their cardinality, as members
       of ` NN0 ` . $)
    hashf $p |- # : Fin --> NN0 $=
      ( vx vy vz cfn cn0 chash wf wfn cfv wcel cc0 com ccrd wceq ax-mp cen con0
      cv wbr wrex wral ffnfv cvv c1 caddc co cmpt crdg cres ccom wfun cdm df-fn
      frfnom fnfun cab cardf2 ffun funco mp2an ccnv cima dmco imaeq2i vex brcnv
      fndm rexbii elima ficardom finnum abid sylibr fdmi syl6eleqr wa eqid fvex
      funbrfvb mpbii sylancr rcla4ev syl2anc wi funbrfv breldm syl6eleq cardid2
      breq2 sylib syl eqbrtrrd ensym sylan2 isfi rexlimiva impbii 3bitr4i eqriv
      eqtri mpbir2an fneq1i mpbir hashgval wf1o hashgf1o f1of ffvelrni eqeltrrd
      df-hash rgen ) DEFGFDHZARZFIZEJZADUAADEFUBXLAUCXMUDUEUFUGZKUHLUIZMUJZDHZX
      SXRUKZXRULZDNXRDUMXQUKZMUKZXTXQLHZYBKXPUNZLXQUOOBRZXMPSBQTZAUPZQMGYCABUQZ
      YHQMUROZXQMUSUTYAMVAZXQULZVBZDXQMVCYMYKLVBZDYLLYKYDYLLNYELXQVGOVDAYNDCRZX
      MYKSZCLTXMYOMSZCLTZXMYNJXMDJZYPYQCLYOXMMCVEZAVEZVFVHCXMYKLUUAVIYSYRYSXMMI
      ZLJZXMUUBMSZYRXMVJZYSYCXMMULZJZUUDYJYSXMYHUUFYSYGXMYHJZBXMVKYGAVLZVMYHQMY
      IVNZVOYCUUGVPUUBUUBNUUDUUBVQXMUUBMXMMVRVSVTWAYQUUDCUUBLYOUUBXMMWIWBWCYQYS
      CLYOLJZYQVPXMYFPSZBLTZYSYQUUKXMYOPSZUUMYQYOXMPSUUNYQUUBYOXMPYCYQUUBYONWDY
      JXMYOMYTWEOYQYGUUBXMPSYQUUHYGYQXMUUFYHXMYOMUUAWFUUJWGUUIWJBXMWHWKWLYOXMUU
      AWMWKUULUUNBYOLYFYOXMPWIWBWNBXMWOVMWPWQWRWSWTWTXADFXRAXJXBXCXOADYSUUBBUCY
      FUDUEUFUGKUHLUIZIZXNEBXMUUOUUOVQZXDYSUUCUUPEJUUELEUUBUUOLEUUOXELEUUOGBUUO
      UUQXFLEUUOXGOXHWKXIXKXA $.
      $( [13-Sep-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d x N $.
    $( The set ` ( 1 ... N ) ` has ` N ` elements.  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    hashfz1 $p |- ( N e. NN0 -> ( # ` ( 1 ... N ) ) = N ) $=
      ( vx cn0 wcel c1 cfz co ccrd cfv cvv cv caddc cmpt cc0 crdg com cres ccnv
      chash wceq eqid cardfz fveq2d fzfid hashgval wf1o hashgf1o f1ocnvfv2 mpan
      cfn syl 3eqtr3d ) ACDZEAFGZHIZBJBKELGMNOPQZIZAUPRIZUPIZUNSIZAUMUOURUPBUPA
      UPUAZUBUCUMUNUJDUQUTTUMEAUDBUNUPVAUEUKPCUPUFUMUSATBUPVAUGPCAUPUHUIUL $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)

    $( Two finite sets have the same number of elements iff they are
       equinumerous.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    hashen $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( ( # ` A ) = ( # ` B ) <-> A ~~ B ) ) $=
      ( vx cfn wcel chash cfv wceq ccrd cen wbr fveq2 hashginv eqeqan12d syl5ib
      wa hashgval impbid wi con0 cvv cv c1 caddc co cmpt cc0 crdg com cres ccnv
      eqid wrex finnum cardid2 syl ensymg mpd breq1 anbi2d biimprcd entr syl2an
      syl6 carden2b adantl bitrd ) ADEZBDEZPZAFGZBFGZHZAIGZBIGZHZABJKZVJVMVPVMV
      KCUACUBZUCUDUEUFUGUHUIUJZUKZGZVLVTGZHVJVPVKVLVTLVHVIWAVNWBVOCAVSVSULZMCBV
      SWCMNOVPVNVSGZVOVSGZHVJVMVNVOVSLVHVIWDVKWEVLCAVSWCQCBVSWCQNORVJVPVQVHAVNJ
      KZVOBJKZVPVQSVIVHVNAJKZWFVHVRAJKCTUMWHCAUNCAUOUPVNADUQURVIVRBJKCTUMWGCBUN
      CBUOUPWFWGPZVPWFVNBJKZPZVQVPWKWIVPWJWGWFVNVOBJUSUTVAAVNBVBVDVCVIVQVPSVHAB
      DVEVFRVG $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  $( Two possibly-empty 1-based finite sets of sequential integers are equal
     iff their endpoints are equal.  (Contributed by Paul Chapman,
     22-Jun-2011.)  (The proof was shortened by Mario Carneiro,
     29-Mar-2014.) $)
  fz1eqb $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
               ( ( 1 ... M ) = ( 1 ... N ) <-> M = N ) ) $=
    ( cn0 wcel wa c1 cfz co wceq chash cfv fveq2 hashfz1 eqeqan12d syl5ib oveq2
    impbid1 ) ACDZBCDZEZFAGHZFBGHZIZABIZUCUAJKZUBJKZITUDUAUBJLRSUEAUFBAMBMNOABF
    GPQ $.
    $( [29-Mar-2014] $) $( [22-Jun-2011] $)

  ${
    $d x A $.
    $( The size function of the cardinality function. $)
    hashcard $p |- ( A e. Fin -> ( # ` ( card ` A ) ) = ( # ` A ) ) $=
      ( vx cfn wcel ccrd cfv cvv cv c1 caddc co cmpt cc0 crdg com chash cardidm
      cres wceq hashgval fveq2i a1i wss ficardom ssid sylancl eqid syl 3eqtr3d
      ssnnfi ) ACDZAEFZEFZBGBHIJKLMNORZFZULUNFZULPFZAPFUOUPSUKUMULUNAQUAUBUKULC
      DZUOUQSUKULODULULUCURAUDULUEULULUJUFBULUNUNUGZTUHBAUNUSTUI $.
      $( [4-Nov-2013] $) $( [19-Sep-2013] $)
  $}

  $( Closure of the ` # ` function.  (Contributed by Paul Chapman,
     26-Oct-2012.)  (Revised by Mario Carneiro, 15-Sep-2013.) $)
  hashcl $p |- ( A e. Fin -> ( # ` A ) e. NN0 ) $=
    ( cfn cn0 chash hashf ffvelrni ) BCADEF $.
    $( [15-Sep-2013] $) $( [26-Oct-2012] $)

  $( Two ways of saying a finite set is empty.  (Contributed by Paul Chapman,
     26-Oct-2012.) $)
  hash0 $p |- ( A e. Fin -> ( ( # ` A ) = 0 <-> A = (/) ) ) $=
    ( chash cfv cc0 wceq c0 cfn wcel c1 cfz co eqid cn0 0nn0 ax-mp mpbir fveq2i
    wb fz1n hashfz1 eqtr3i eqeq2i cen wbr 0fin hashen mpan2 en0 syl6bb syl5bbr
    ) ABCZDEUKFBCZEZAGHZAFEZULDUKIDJKZBCZULDUPFBUPFEZDDEZDLDMHZURUSRNDSOPQUTUQD
    ENDTOUAUBUNUMAFUCUDZUOUNFGHUMVARUEAFUFUGAUHUIUJ $.
    $( [26-Oct-2012] $)

  $( The size of a singleton.  (Contributed by Paul Chapman, 26-Oct-2012.)
     (The proof was shortened by Mario Carneiro, 13-Feb-2013.) $)
  hashsng $p |- ( A e. V -> ( # ` { A } ) = 1 ) $=
    ( wcel csn chash cfv c1 cen wbr wceq cz 1z en2sn mpan2 wb snfi hashen mp2an
    cfn ax-mp sylibr cfz co cn0 1nn0 hashfz1 fzsn fveq2d syl5reqr syl6eq ) ABCZ
    ADZEFZGDZEFZGUKULUNHIZUMUOJZUKGKCZUPLAGBKMNULSCUNSCUQUPOAPGPULUNQRUAURUOGJL
    URGGGUBUCZEFZUOGUDCUTGJUEGUFTURUSUNEGUGUHUITUJ $.
    $( [13-Feb-2013] $) $( [26-Oct-2012] $)

  $( The value of the size function on a singleton.  (Contributed by Paul
     Chapman, 26-Oct-2012.) $)
  hashsngOLD $p |- ( A e. V -> ( # ` { A } ) = 1 ) $=
    ( wcel csn c1 cen wbr chash cfv wceq cz 1z en2sn mpan2 cfz co fzsn cfn snfi
    ax-mp fveq2i cn0 1nn0 hashfz1 eqtr3i eqeq2i wb hashen mp2an bitr3i sylibr )
    ABCZADZEDZFGZUMHIZEJZULEKCZUOLAEBKMNUQUPUNHIZJZUOUSEUPEEOPZHIZUSEVAUNHURVAU
    NJLEQTUAEUBCVBEJUCEUDTUEUFUMRCUNRCUTUOUGASESUMUNUHUIUJUK $.
    $( [26-Oct-2012] $)

  ${
    $d F f $.  $d N f $.
    $( The value of the size function on a finite 1-based sequence.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    fseq1hash $p |- ( ( N e. NN0 /\ F Fn ( 1 ... N ) ) -> ( # ` F ) = N ) $=
      ( vf cvv wcel cn0 c1 cfz wfn wa chash cfv wceq adantl cen wbr cfn sylancr
      wi wb co ovex fnex mpan2 fneq1 anbi2d fveq2 eqeq1d imbi12d cdm fndm fnfun
      cv wfun vex fundmen eqbrtrrd fzfi enfi mpbii hashen mpbird hashfz1 adantr
      syl eqtr3d vtoclg mpcom ) ADEZBFEZAGBHUAZIZJZAKLZBMZVLVIVJVLVKDEVIGBHUBVK
      DAUCUDNVJCUMZVKIZJZVPKLZBMZSVMVOSCADVPAMZVRVMVTVOWAVQVLVJVKVPAUEUFWAVSVNB
      VPAKUGUHUIVRVKKLZVSBVRWBVSMZVKVPOPZVQWDVJVQVPUJZVKVPOVKVPUKVQVPUNWEVPOPVK
      VPULVPCUOZUPVEUQNZVRVKQEZVPQEZWCWDTGBURZVRWHWIWJVRVPDEWDWHWITWFWGVKVPDUSR
      UTVKVPVARVBVJWBBMVQBVCVDVFVGVH $.
      $( [26-Oct-2012] $)
  $}

  ${
    $d A n z $.  $d B n $.  $d G n z $.
    hashgadd.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    $( ` G ` maps ordinal addition to integer addition.  (Contributed by Paul
       Chapman, 30-Nov-2012.) $)
    hashgadd $p |- ( ( A e. om /\ B e. om ) ->
                     ( G ` ( A +o B ) ) = ( ( G ` A ) + ( G ` B ) ) ) $=
      ( com wcel coa co cfv caddc wceq wi c0 fveq2d oveq2d cc0 cn0 syl c1 vn vz
      cv csuc oveq2 fveq2 eqeq12d cc wf1o wf hashgf1o f1of ax-mp ffvelrni nn0cn
      imbi2d addid1 0z om2uz0i oveq2i a1i con0 oa0 3eqtr4rd w3a wa oasuc syl2an
      nnon nnacl om2uzsuci eqtrd 3adant3 ax-1cn addass mp3an3 3ad2ant3 3ad2ant2
      oveq1 3eqtr4d 3expia expcom a2d finds impcom ) CFGBFGZBCHIZDJZBDJZCDJZKIZ
      LZWFBUAUCZHIZDJZWIWMDJZKIZLZMWFBNHIZDJZWINDJZKIZLZMWFBUBUCZHIZDJZWIXDDJZK
      IZLZMWFBXDUDZHIZDJZWIXJDJZKIZLZMWFWLMUAUBCWMNLZWRXCWFXPWOWTWQXBXPWNWSDWMN
      BHUEOXPWPXAWIKWMNDUFPUGUPWMXDLZWRXIWFXQWOXFWQXHXQWNXEDWMXDBHUEOXQWPXGWIKW
      MXDDUFPUGUPWMXJLZWRXOWFXRWOXLWQXNXRWNXKDWMXJBHUEOXRWPXMWIKWMXJDUFPUGUPWMC
      LZWRWLWFXSWOWHWQWKXSWNWGDWMCBHUEOXSWPWJWIKWMCDUFPUGUPWFWIQKIZWIXBWTWFWIUH
      GZXTWILWFWIRGYAFRBDFRDUIFRDUJADEUKFRDULUMZUNWIUOSZWIUQSXBXTLWFXAQWIKAQDUR
      EUSUTVAWFWSBDWFBVBGZWSBLBVIZBVCSOVDXDFGZWFXIXOWFYFXIXOMWFYFXIXOWFYFXIVEZX
      LXFTKIZXNWFYFXLYHLXIWFYFVFZXLXEUDZDJZYHYIXKYJDWFYDXDVBGXKYJLYFYEXDVIBXDVG
      VHOYIXEFGYKYHLBXDVJAXEQDUREVKSVLVMYGXHTKIZWIXGTKIZKIZYHXNWFYFYLYNLZXIWFYA
      XGUHGZYOYFYCYFXGRGYPFRXDDYBUNXGUOSYAYPTUHGYOVNWIXGTVOVPVHVMXIWFYHYLLYFXFX
      HTKVSVQYFWFXNYNLXIYFXMYMWIKAXDQDUREVKPVRVTVLWAWBWCWDWE $.
      $( [15-Sep-2013] $) $( [30-Nov-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Dominance relation for the size function. $)
    hashdom $p |- ( ( A e. Fin /\ B e. Fin )
         -> ( ( # ` A ) <_ ( # ` B ) <-> A ~<_ B ) ) $=
      ( vy vx cfn wcel wa ccrd cfv coa co wceq com wbr chash cle ficardom caddc
      cn0 syl wss cv wrex cdom wb nnawordex syl2an ficarddom c1 cmin fzfi ax-mp
      cfz cvv cmpt cc0 crdg cres ccnv hashgval ad2antrr hashcl ad2antlr nn0sub2
      eqid simpr syl3anc hashfz1 syl5eq oveq12d cc nn0cn pncan3 adantr hashgadd
      eqtrd sylancl 3eqtr4d wf1o hashgf1o nnacl f1ocnvfv1 sylancr 3eqtr3d oveq2
      fveq2d eqeq1d rcla4ev ex cardnn adantl oveq2d fveq2 ssid ssnnfi oveqan12d
      wi mpan2 adantlr eqeq12d nn0ge0 nn0re mpbid breq2 syl5ibcom sylbid sylan2
      cr addge01 syl5 sylbird rexlimdva impbid 3bitr4rd ) AEFZBEFZGZAHIZBHIZUAZ
      XRCUBZJKZXSLZCMUCZABUDNAOIZBOIZPNZXOXRMFZXSMFZXTYDUEXPAQZBQZCXRXSUFUGABUH
      XQYGYDXQYGYDXQYGGZUIYFYEUJKZUMKZHIZMFZXRYOJKZXSLZYDYNEFZYPUIYMUKZYNQULZYL
      YQDUNDUBUIRKUOUPUQMURZIZUUBUSZIZXSUUBIZUUDIZYQXSYLUUCUUFUUDYLXRUUBIZYOUUB
      IZRKZYFUUCUUFYLUUJYEYMRKZYFYLUUHYEUUIYMRXOUUHYELXPYGDAUUBUUBVEZUTZVAYLUUI
      YNOIZYMYSUUIUUNLYTDYNUUBUULUTULYLYMSFZUUNYMLYLYESFZYFSFZYGUUOXOUUPXPYGAVB
      ZVAXPUUQXOYGBVBZVCXQYGVFYEYFVDVGYMVHTVIVJXQUUKYFLZYGXOYEVKFZYFVKFZUUTXPXO
      UUPUVAUURYEVLTXPUUQUVBUUSYFVLTYEYFVMUGVNVPYLYHYPUUCUUJLXOYHXPYGYJVAZUUADX
      RYOUUBUULVOVQXPUUFYFLZXOYGDBUUBUULUTZVCVRWFYLMSUUBVSZYQMFZUUEYQLDUUBUULVT
      ZYLYHYPUVGUVCUUAXRYOWAVQMSYQUUBWBWCYLUVFYIUUGXSLUVHXPYIXOYGYKVCMSXSUUBWBW
      CWDYCYRCYOMYAYOLYBYQXSYAYOXRJWEWGWHWCWIXQYCYGCMXQYAMFZGZYCXRYAHIZJKZXSLZY
      GUVJUVLYBXSUVJUVKYAXRJUVIUVKYALXQYAWJWKWLWGUVMUVLUUBIZUUFLZUVJYGUVLXSUUBW
      MUVIXQYAEFZUVOYGWQUVIYAYAUAUVPYAWNYAYAWOWRXQUVPGZUVOYEYAOIZRKZYFLZYGUVQUV
      NUVSUUFYFXOUVPUVNUVSLXPXOUVPGZUVNUUHUVKUUBIZRKZUVSXOYHUVKMFUVNUWCLUVPYJYA
      QDXRUVKUUBUULVOUGXOUVPUUHYEUWBUVRRUUMDYAUUBUULUTWPVPWSXPUVDXOUVPUVEVCWTUV
      QYEUVSPNZUVTYGXOUVPUWDXPUWAUPUVRPNZUWDUVPUWEXOUVPUVRSFZUWEYAVBZUVRXATWKXO
      YEXHFZUVRXHFZUWEUWDUEUVPXOUUPUWHUURYEXBTUVPUWFUWIUWGUVRXBTYEUVRXIUGXCWSUV
      SYFYEPXDXEXFXGXJXKXLXMXN $.
      $( [14-Feb-2014] $) $( [22-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The size of the union of disjoint finite sets is the sum of their
       sizes.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    hashun $p |- ( ( A e. Fin /\ B e. Fin /\ ( A i^i B ) = (/) ) ->
                   ( # ` ( A u. B ) ) = ( ( # ` A ) + ( # ` B ) ) ) $=
      ( vx cfn wcel cin c0 wceq w3a cun ccrd cfv cvv caddc co com chash 3adant3
      hashgval ficardom cv cmpt cc0 crdg cres coa ficardun fveq2d unfi eqid syl
      c1 wa hashgadd syl2an oveqan12d eqtrd 3eqtr3d ) ADEZBDEZABFGHZIZABJZKLZCM
      CUAULNOUBUCUDPUEZLZAKLZBKLZUFOZVELZVCQLZAQLZBQLZNOZVBVDVIVEABUGUHUSUTVFVK
      HZVAUSUTUMZVCDEVOABUICVCVEVEUJZSUKRUSUTVJVNHVAVPVJVGVELZVHVELZNOZVNUSVGPE
      VHPEVJVTHUTATBTCVGVHVEVQUNUOUSUTVRVLVSVMNCAVEVQSCBVEVQSUPUQRUR $.
      $( [15-Sep-2013] $) $( [30-Nov-2012] $)

    $( The size of the union of finite sets is less than or equal to the sum of
       their sizes. $)
    hashun2 $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( # ` ( A u. B ) ) <_ ( ( # ` A ) + ( # ` B ) ) ) $=
      ( vx cfn wcel chash cfv ccrd co caddc cle wbr wss com ficardom syl2an syl
      wceq wb hashgval cun coa cdom ficardun2 nnacl cardnn sseqtr4d unfi ssnnfi
      wa ssid sylancl ficarddom syl2anc mpbird hashdom cvv cv c1 cmpt crdg cres
      cc0 fveq2d eqid hashgadd 3eqtr3d oveqan12d eqtrd breqtrd ) ADEZBDEZUJZABU
      AZFGZAHGZBHGZUBIZFGZAFGZBFGZJIZKVMVOVSKLZVNVRUCLZVMWDVNHGZVRHGZMZVMWEVRWF
      ABUDVMVRNEZWFVRRVKVPNEZVQNEZWHVLAOZBOZVPVQUEPZVRUFQZUGVMVNDEZVRDEZWDWGSAB
      UHZVMWHVRVRMWPWMVRUKVRVRUIULZVNVRUMUNUOVMWOWPWCWDSWQWRVNVRUPUNUOVMVSVPCUQ
      CURUSJIUTVCVANVBZGZVQWSGZJIZWBVMWFWSGZVRWSGZVSXBVMWFVRWSWNVDVMWPXCVSRWRCV
      RWSWSVEZTQVKWIWJXDXBRVLWKWLCVPVQWSXEVFPVGVKVLWTVTXAWAJCAWSXETCBWSXETVHVIV
      J $.
      $( [4-Nov-2013] $) $( [23-Sep-2013] $)
  $}

  ${
    hashpr.1 $e |- A e. _V $.
    hashpr.2 $e |- B e. _V $.
    $( The size of an unordered pair. $)
    hashpr $p |- ( A =/= B <-> ( # ` { A , B } ) = 2 ) $=
      ( wne cpr chash cfv c2 wceq c1 caddc csn cfn wcel snfi cvv hashsng ax-mp
      co cun df-pr fveq2i cin c0 disjsn2 hashun mp3an12 syl syl5eq oveq12i df-2
      syl6eq syl6eqr dfsn2 preq2 syl5req fveq2d 1ne2 eqnetri a1i eqnetrd impbii
      necon2i ) ABEZABFZGHZIJVEVGKKLTZIVEVGAMZGHZBMZGHZLTZVHVEVGVIVKUAZGHZVMVFV
      NGABUBUCVEVIVKUDUEJZVOVMJZABUFVINOVKNOVPVQAPBPVIVKUGUHUIUJVJKVLKLAQOVJKJC
      AQRSZBQOVLKJDBQRSUKUMULUNABVGIABJZVGVJIVSVFVIGVSVIAAFVFAUOABAUPUQURVJIEVS
      VJKIVRUSUTVAVBVDVC $.
      $( [27-Sep-2013] $)
  $}

  $( The size of the union of a finite set with a disjoint singleton is one
     more than the size of the set.  (Contributed by Paul Chapman,
     30-Nov-2012.) $)
  hashunsng $p |- ( B e. V -> ( ( A e. Fin /\ -. B e. A ) ->
                    ( # ` ( A u. { B } ) ) = ( ( # ` A ) + 1 ) ) ) $=
    ( cfn wcel wn wa csn cun chash cfv c1 caddc co wceq cin disjsn snfi hashun
    c0 mp3an2 sylan2br hashsng oveq2d sylan9eq expcom ) ADEZBAEFZGZBCEZABHZIJKZ
    AJKZLMNZOUIUJULUMUKJKZMNZUNUHUGAUKPTOZULUPOZABQUGUKDEUQURBRAUKSUAUBUJUOLUMM
    BCUCUDUEUF $.
    $( [30-Nov-2012] $)

  ${
    $d A x y z $.  $d B x y z $.
    hashxplem.1 $e |- B e. Fin $.
    $( Lemma for ~ hashxp . $)
    hashxplem $p |- ( A e. Fin ->
                      ( # ` ( A X. B ) ) = ( ( # ` A ) x. ( # ` B ) ) ) $=
      ( cxp chash cfv cmul co wceq c0 xpeq1 fveq2d fveq2 oveq1d eqeq12d cc0 cfn
      wcel ax-mp caddc vx vy vz cv csn cun cc hashcl nn0cn mul02 3syl 0fin eqid
      cn0 hash0 biimpar mp2an oveq1i xp0r fveq2i eqtri 3eqtr4ri wn oveq1 adantl
      wa xpundir cin xpfi mpan2 inxp disjsn biimpri syl6eq syl5eq hashun mp3an2
      syl snfi syl2an cen wbr snex elexi xpcomen vex xpsnen entri hashen oveq2i
      wb mpbir adantr c1 cvv wi hashunsng ax-1cn adddir mp3an23 mulid2 eqtrd ex
      3eqtr4d findcard2s ) UAUDZBDZEFZXFEFZBEFZGHZIJBDZEFZJEFZXJGHZIUBUDZBDZEFZ
      XPEFZXJGHZIZXPUCUDZUEZUFZBDZEFZYDEFZXJGHZIZABDZEFZAEFZXJGHZIUAUBUCAXFJIZX
      HXMXKXOYNXGXLEXFJBKLYNXIXNXJGXFJEMNOXFXPIZXHXRXKXTYOXGXQEXFXPBKLYOXIXSXJG
      XFXPEMNOXFYDIZXHYFXKYHYPXGYEEXFYDBKLYPXIYGXJGXFYDEMNOXFAIZXHYKXKYMYQXGYJE
      XFABKLYQXIYLXJGXFAEMNOPXJGHZPXOXMBQRZYRPIZCYSXJUNRZXJUGRZYTBUHZXJUIZXJUJU
      KSXNPXJGJQRZJJIZXNPIZULJUMUUEUUGUUFJUOUPUQZURXMXNPXLJEBUSUTUUHVAVBXPQRZYB
      XPRVCZVFZYAYIUUKYAVFXRXJTHZXTXJTHZYFYHYAUULUUMIUUKXRXTXJTVDVEUUKYFUULIYAU
      UKYFXQYCBDZUFZEFZUULYEUUOEXPYCBVGUTUUKUUPXRUUNEFZTHZUULUUIXQQRZXQUUNVHZJI
      ZUUPUURIZUUJUUIYSUUSCXPBVIVJUUJUUTXPYCVHZBBVHZDZJXPBYCBVKUUJUVEJUVDDZJUUJ
      UVCJIZUVEUVFIUVGUUJXPYBVLVMUVCJUVDKVRUVDUSVNVOUUSUUNQRZUVAUVBYCQRYSUVHYBV
      SCYCBVIUQZXQUUNVPVQVTUUQXJXRTUUQXJIZUUNBWAWBZUUNBYCDBYCBYBWCBQCWDZWEBYBUV
      LUCWFZWGWHUVHYSUVJUVKWKUVICUUNBWIUQWLWJVNVOWMUUKYHUUMIYAUUKYHXSWNTHZXJGHZ
      UUMUUKYGUVNXJGYBWORUUKYGUVNIWPUVMXPYBWOWQSNUUIUVOUUMIUUJUUIUVOXTWNXJGHZTH
      ZUUMUUIXSUNRXSUGRZUVOUVQIZXPUHXSUIUVRWNUGRUUBUVSWRYSUUBCYSUUAUUBUUCUUDVRS
      ZXSWNXJWSWTUKUVPXJXTTUUBUVPXJIUVTXJXASWJVNWMXBWMXDXCXE $.
      $( [30-Nov-2012] $)
  $}

  $( The size of the Cartesian product of two finite sets is the product of
     their sizes.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
  hashxp $p |- ( ( A e. Fin /\ B e. Fin ) ->
                 ( # ` ( A X. B ) ) = ( ( # ` A ) x. ( # ` B ) ) ) $=
    ( cfn wcel cxp chash cfv cmul co wceq wi c0 cif xpeq2 fveq2d oveq2d eqeq12d
    fveq2 imbi2d 0fin elimel hashxplem dedth impcom ) BCDZACDZABEZFGZAFGZBFGZHI
    ZJZUEUFULKUFAUEBLMZEZFGZUIUMFGZHIZJZKBLBUMJZULURUFUSUHUOUKUQUSUGUNFBUMANOUS
    UJUPUIHBUMFRPQSAUMBLCTUAUBUCUD $.
    $( [30-Nov-2012] $)

  ${
    $d F a b $.  $d S a b $.  $d Y a b w $.  $d Z a b w $.
    hashpwlem.1 $e |- F = ( w e. ~P Y |-> ( w u. { Z } ) ) $.
    hashpwlem.2 $e |- S = { w | ( w \ Y ) = { Z } } $.
    $( Lemma for ~ hashpw . $)
    hashpwlem $p |- ( -. Z e. Y -> F : ~P Y -1-1-onto-> S ) $=
      ( va vb wcel wceq wral cun unex wa cdif c0 wss biimpi cin wn crn cfv wf1o
      cv wi cpw wfo wrex wfn csn vex snex cmpt copab df-mpt eqtri fnopab2 uneq1
      wf a1i fvmpt adantl difundir elpw ssdif0 bitri disjsn incom eqeq1i bitr3i
      disj3 eqcomd adantr uneq12d uncom un0 syl6eq syl5eq difeq1 eqeq1d eqeltrd
      elab2 sylibr ralrimiva ffnfv inss2 dfin4 difeq2 syl5req sseq1d mpbiri cvv
      sylanbrc difexg ax-mp syl undif1 difss sseq1 mpbii ssequn1 syl5eqr eqtr2d
      sylbi fveq2 eqeq2d rcla4ev rgen dffo3 forn eqeqan12d anbi12i ssel con3rr3
      jca wb syl6ibr anim12d eqtr3 syl6 imp sylan2b unineq sylan2 expcom sylbid
      ralrimivva w3a dff1o6 biimpri mp3an1 syl2anc ) EDJZUAZCUBBKZHUEZCUCZIUEZC
      UCZKZYQYSKZUFZIDUGZLHUUDLZUUDBCUDZYOUUDBCUHZYPYOUUDBCUTZYQYTKZIUUDUIZHBLZ
      UUGYOCUUDUJZYRBJZHUUDLUUHUULYOAHUUDAUEZEUKZMZCUUNUUOAULEUMZNCAUUDUUPUNUUN
      UUDJYQUUPKOAHUOFAHUUDUUPUPUQURZVAYOUUMHUUDYOYQUUDJZOZYRYQUUOMZBUUSYRUVAKY
      OAYQUUPUVAUUDCUUNYQUUOUSFYQUUOHULZUUQNZVBZVCUUTUVADPZUUOKZUVABJUUTUVEYQDP
      ZUUODPZMZUUOYQUUODVDUUTUVIQUUOMZUUOUUTUVGQUVHUUOUUSUVGQKZYOUUSUVKUUSYQDRZ
      UVKYQDUVBVEZYQDVFVGSVCYOUVHUUOKUUSYOUUOUVHYOUUOUVHKZYOUUODTZQKZUVNYODUUOT
      ZQKUVPDEVHUVQUVOQDUUOVIVJVKUUODVLVGSVMVNVOUVJUUOQMUUOQUUOVPUUOVQUQVRVSUUN
      DPZUUOKZUVFAUVABUVCUUNUVAKUVRUVEUUOUUNUVADVTWAGWCWDWBWEHUUDBCWFWNUUKYOUUJ
      HBYQBJZYQUUOPZUUDJZYQUWACUCZKZOZUUJUVTUVGUUOKZUWEUVSUWFAYQBUVBUUNYQKUVRUV
      GUUOUUNYQDVTWAGWCUWFUWBUWDUWFUWADRZUWBUWFUWGYQDTZDRYQDWGUWFUWAUWHDUWFUWHY
      QUVGPUWAYQDWHUVGUUOYQWIWJWKWLUWADYQWMJUWAWMJUVBYQUUOWMWOWPZVEWDZUWFUWCUWA
      UUOMZYQUWFUWBUWCUWKKUWJAUWAUUPUWKUUDCUUNUWAUUOUSFUWAUUOUWIUUQNVBWQUWFUWKU
      VAYQYQUUOWRUWFUUOYQRZUVAYQKUWFUVGYQRUWLYQDWSUVGUUOYQWTXAUWLUVAUUOYQMZYQUU
      OYQVPUWLUWMYQKUUOYQXBSXCWQVSXDXPXEUUIUWDIUWAUUDYSUWAKYTUWCYQYSUWACXFXGXHW
      QXIVAIHUUDBCXJWNUUDBCXKWQYOUUCHIUUDUUDYOUUSYSUUDJZOZOZUUAUVAYSUUOMZKZUUBU
      WOUUAUWRXQYOUUSUWNYRUVAYTUWQUVDAYSUUPUWQUUDCUUNYSUUOUSFYSUUOIULZUUQNVBXLV
      CUWRUWPUUBUWPUWRYQUUOTZYSUUOTZKZUUBUWOYOUVLYSDRZOZUXBUUSUVLUWNUXCUVMYSDUW
      SVEXMYOUXDUXBYOUXDUWTQKZUXAQKZOUXBYOUVLUXEUXCUXFYOUVLEYQJZUAUXEUVLUXGYNYQ
      DEXNXOYQEVHXRYOUXCEYSJZUAUXFUXCUXHYNYSDEXNXOYSEVHXRXSUWTUXAQXTYAYBYCUWRUX
      BOUUBYQYSUUOYDSYEYFYGYHUULYPUUEUUFUURUUFUULYPUUEYIHIUUDBCYJYKYLYM $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d A x y z $.  $d w x y z $.
    $( The size of the power set of a finite set is 2 raised to the power of
       the size of the set.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    hashpw $p |- ( A e. Fin -> ( # ` ~P A ) = ( 2 ^ ( # ` A ) ) ) $=
      ( vx vw cv cpw chash cfv c2 cexp co wceq c0 cun pweq oveq2d c1 cfn adantr
      wcel cab vy vz csn fveq2d fveq2 eqeq12d cc0 cc exp0 ax-mp 0fin eqid hash0
      2cn biimpar mp2an oveq2i pw0 fveq2i hashsng eqtri 3eqtr4ri wn cmul adantl
      wa oveq1 caddc cdif cen wbr cmpt wf1o hashpwlem vex pwex f1oen syl biimpi
      wb pwfi crn cdom cvv wfn snex df-mpt fnopab2 fnrndomg mp2 domfi mpan2 wfo
      unex f1ofo forn 3syl eleq1d mpbid hashen syl2anc mpbird cn0 hashcl times2
      nn0cn wo ssundif sssn bitri abbii df-pw unab 3eqtr4i ssdif0 uneq1i eqtr4i
      wss cin ineq1i inab snprc con1bii mpbir eqeq1 mtbiri con2i imnan mpbi abf
      wi hashun mp3an3 syl5eq 3eqtr4rd hashunsng expp1 mpan eqtrd 3eqtr4d ex
      findcard2s ) BDZEZFGZHUUCFGZIJZKLEZFGZHLFGZIJZKUADZEZFGZHUULFGZIJZKZUULUB
      DZUCZMZEZFGZHUUTFGZIJZKZAEZFGZHAFGZIJZKBUAUBAUUCLKZUUEUUIUUGUUKUVJUUDUUHF
      UUCLNUDUVJUUFUUJHIUUCLFUEOUFUUCUULKZUUEUUNUUGUUPUVKUUDUUMFUUCUULNUDUVKUUF
      UUOHIUUCUULFUEOUFUUCUUTKZUUEUVBUUGUVDUVLUUDUVAFUUCUUTNUDUVLUUFUVCHIUUCUUT
      FUEOUFUUCAKZUUEUVGUUGUVIUVMUUDUVFFUUCANUDUVMUUFUVHHIUUCAFUEOUFHUGIJZPUUKU
      UIHUHSZUVNPKUNHUIUJUUJUGHILQSZLLKZUUJUGKZUKLULUVPUVRUVQLUMUOUPUQUUILUCZFG
      ZPUUHUVSFURUSUVPUVTPKUKLQUTUJVAVBUULQSZUURUULSVCZVFZUUQUVEUWCUUQVFUUNHVDJ
      ZUUPHVDJZUVBUVDUUQUWDUWEKUWCUUNUUPHVDVGVEUWCUVBUWDKUUQUWCUUNUUNVHJZUUNCDZ
      UULVIZUUSKZCTZFGZVHJZUWDUVBUWCUUNUWKUUNVHUWCUUNUWKKZUUMUWJVJVKZUWBUWNUWAU
      WBUUMUWJCUUMUWGUUSMZVLZVMZUWNCUWJUWPUULUURUWPULUWJULVNZUUMUWJUWPUULUAVOVP
      ZVQVRVEUWCUUMQSZUWJQSZUWMUWNVTUWAUWTUWBUWAUWTUULWAVSZRZUWCUWPWBZQSZUXAUWA
      UXEUWBUWAUWTUXEUXBUWTUXDUUMWCVKZUXEUUMWDSUWPUUMWEUXFUWSCBUUMUWOUWPUWGUUSC
      VOUURWFWNCBUUMUWOWGWHUUMWDUWPWIWJUUMUXDWKWLVRRUWBUXEUXAVTUWAUWBUXDUWJQUWB
      UWQUUMUWJUWPWMUXDUWJKUWRUUMUWJUWPWOUUMUWJUWPWPWQWRVEWSZUUMUWJWTXAXBOUWCUW
      TUWDUWFKZUXCUWTUUNXCSUUNUHSUXHUUMXDUUNXFUUNXEWQVRUWCUVBUUMUWJMZFGZUWLUVAU
      XIFUVAUWHLKZCTZUWJMZUXIUWGUUTXRZCTUXKUWIXGZCTUVAUXMUXNUXOCUXNUWHUUSXRUXOU
      WGUULUUSXHUWHUURXIXJXKCUUTXLUXKUWICXMXNUUMUXLUWJUUMUWGUULXRZCTUXLCUULXLUX
      PUXKCUWGUULXOXKVAZXPXQUSUWCUWTUXAUXJUWLKZUXCUXGUWTUXAUUMUWJXSZLKUXRUXSUXL
      UWJXSZLUUMUXLUWJUXQXTUXTUXKUWIVFZCTLUXKUWICYAUYACUXKUWIVCYKUYAVCUWIUXKUWI
      UXKUUSLKZUYBVCUURWDSZUBVOZUYCUYBUURYBYCYDUWHUUSLYEYFYGUXKUWIYHYIYJVAVAUUM
      UWJYLYMXAYNYORUWCUVDUWEKUUQUWCUVDHUUOPVHJZIJZUWEUWCUVCUYEHIUYCUWCUVCUYEKY
      KUYDUULUURWDYPUJOUWAUYFUWEKZUWBUWAUUOXCSZUYGUULXDUVOUYHUYGUNHUUOYQYRVRRYS
      RYTUUAUUB $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d x y z F $.
    $( A finite set is a function iff it is equinumerous to its domain. $)
    hashfun $p |- ( F e. Fin -> ( Fun F <-> ( # ` F ) = ( # ` dom F ) ) ) $=
      ( vx vy vz cfn wcel chash cfv cdm wceq wbr adantr cr clt syl c1 caddc cle
      c0 c2 wfun wa cen simpl fundmeng ensymg sylc wb dmfi hashen syldan mpbird
      wrel cv cop wi wal wn wne cn0 hashcl nn0re cvv cxp wrex wral df-rel dfss3
      wss bitri notbii rexnal bitr4i w3a csn cdif co dmun fveq2i dmsnn0 biimpri
      cun necon1bi 3ad2ant3 uneq2d un0 syl6eq fveq2d syl5eq diffi peano2re cdom
      fidomdm hashdom syl2anc ltp1 lelttrd 3ad2ant1 eqbrtrd incom disjdif eqtri
      cin hashun mp3an23 vex hashsng ax-mp oveq2i syl6req breqtrd difsnid dmeqd
      snfi 3ad2ant2 3brtr3d 3exp rexlimdv imp ltne syl3anc necon4bd wex 2nalexn
      syl5bi df-ne anbi2i annim exbii bitr2i 2exbii cpr 2re readdcl sylancr 1re
      ex exnal opex ad2antrl undif biimpi syl5reqr dmprop eqtr4i uneq1i hashun2
      prss dfsn2 oveq1i syl6breq 1lt2 ltadd1 mp3an12 mpbii leadd2 mpbid mp3an13
      mp3an3 prfi opth2 necon3i hashpr oveq1d ad2antll 3eqtr3rd ltletrd exlimdv
      sylib exlimdvv dffun4 sylanbrc impbida ) AEFZAUAZAGHZAIZGHZJZUVNUVOUBZUVS
      AUVQUCKZUVTUVNUVQAUCKUWAUVNUVOUDAEUEUVQAEUFUGUVNUVOUVQEFZUVSUWAUHUVNUWBUV
      OAUIZLAUVQUJUKULUVNUVSUBAUMZBUNZCUNZUOZAFUWEDUNZUOZAFUBZUWFUWHJZUPZDUQZCU
      QBUQZUVOUVNUVSUWDUVNUWDUVPUVRUVNUWDURZUVPUVRUSZUVNUWOUBUVRMFZUVPMFZUVRUVP
      NKZUWPUVNUWQUWOUVNUVRUTFZUWQUVNUWBUWTUWCUVQVAOUVRVBOZLUVNUWRUWOUVNUVPUTFU
      WRAVAUVPVBOZLUVNUWOUWSUWOUWEVCVCVDZFZURZBAVEZUVNUWSUWOUXDBAVFZURUXFUWDUXG
      UWDAUXCVIUXGAVGBAUXCVHVJVKUXDBAVLVMUVNUXEUWSBAUVNUWEAFZUXEUWSUVNUXHUXEVNZ
      AUWEVOZVPZUXJWBZIZGHZUXLGHZUVRUVPNUXIUXNUXKGHZPQVQZUXONUXIUXNUXKIZGHZUXQN
      UXIUXNUXRUXJIZWBZGHUXSUXMUYAGUXKUXJVRVSUXIUYAUXRGUXIUYAUXRSWBUXRUXIUXTSUX
      RUXEUVNUXTSJUXHUXDUXTSUXDUXTSUSUWEVTWAWCWDWEUXRWFWGWHWIUVNUXHUXSUXQNKUXEU
      VNUXSUXPUXQUVNUXSUTFZUXSMFUVNUXREFZUYBUVNUXKEFZUYCAUXJWJZUXKUIOZUXRVAOUXS
      VBOUVNUXPUTFZUXPMFZUVNUYDUYGUYEUXKVAOUXPVBOZUVNUYHUXQMFUYIUXPWKOUVNUXSUXP
      RKZUXRUXKWLKZUVNUYDUYKUYEUXKWMOUVNUYCUYDUYJUYKUHUYFUYEUXRUXKWNWOULUVNUYHU
      XPUXQNKUYIUXPWPOWQWRWSUVNUXHUXQUXOJUXEUVNUXOUXPUXJGHZQVQZUXQUVNUYDUXOUYMJ
      ZUYEUYDUXJEFZUXKUXJXCZSJUYNUWEXNZUYPUXJUXKXCSUXKUXJWTUXJAXAXBUXKUXJXDXEOU
      YLPUXPQUWEVCFUYLPJBXFUWEVCXGXHZXIXJWRXKUXHUVNUXNUVRJUXEUXHUXMUVQGUXHUXLAA
      UWEXLZXMWHXOUXHUVNUXOUVPJUXEUXHUXLAGUYSWHXOXPXQXRYEXSUVRUVPXTZYAYQYBXSUVN
      UVSUWNUVNUWNUVPUVRUWNURZUWJUWFUWHUSZUBZDYCZCYCBYCZUVNUWPVUAUWMURZCYCBYCVU
      EUWMBCYDVUFVUDBCVUDUWLURZDYCVUFVUCVUGDVUCUWJUWKURZUBVUGVUBVUHUWJUWFUWHYFY
      GUWJUWKYHVJYIUWLDYRYJYKVJUVNVUDUWPBCUVNVUCUWPDUVNVUCUWPUVNVUCUBZUWQUWRUWS
      UWPUVNUWQVUCUXALZUVNUWRVUCUXBLZVUIUVRTAUWGUWIYLZVPZIZGHZQVQZUVPVUJVUITMFZ
      VUOMFZVUPMFZYMUVNVURVUCUVNVUOUTFZVURUVNVUNEFZVUTUVNVUMEFZVVAAVULWJZVUMUIO
      ZVUNVAOVUOVBOZLTVUOYNZYOVUKVUIUVRPVUOQVQZVUPVUJUVNVVGMFZVUCUVNPMFZVURVVHY
      PVVEPVUOYNYOLUVNVUSVUCUVNVUQVURVUSYMVVEVVFYOLVUIUVRUXJVUNWBZGHZVVGRUWJUVR
      VVKJUVNVUBUWJUVQVVJGUWJUVQVULIZVUNWBZVVJUWJVVMVULVUMWBZIUVQVULVUMVRUWJVVN
      AUWJVVNAJZUWJVULAVIVVOUWGUWIAUWEUWFYSZUWEUWHYSZUUHVULAUUAVJUUBZXMUUCVVLUX
      JVUNVVLUWEUWEYLUXJUWEUWEUWFUWHUUDUWEUUIUUEUUFWGWHYTUVNVVKVVGRKVUCUVNVVKUY
      LVUOQVQZVVGRUVNUYOVVAVVKVVSRKUYQVVDUXJVUNUUGYOUYLPVUOQUYRUUJUUKLWSUVNVVGV
      UPNKZVUCUVNPTNKZVVTUULUVNVURVWAVVTUHZVVEVVIVUQVURVWBYPYMPTVUOUUMUUNOUUOLW
      QVUIVUPTVUMGHZQVQZUVPRUVNVUPVWDRKZVUCUVNVUOVWCRKZVWEUVNVWFVUNVUMWLKZUVNVV
      BVWGVVCVUMWMOUVNVVAVVBVWFVWGUHVVDVVCVUNVUMWNWOULUVNVURVWCMFZVWFVWEUHZVVEU
      VNVWCUTFZVWHUVNVVBVWJVVCVUMVAOVWCVBOVURVWHVUQVWIYMVUOVWCTUUPUUSWOUUQLVUIV
      VNGHZVULGHZVWCQVQZUVPVWDUVNVWKVWMJZVUCUVNVVBVWNVVCVULEFVVBVULVUMXCSJVWNUW
      GUWIUUTVULAXAVULVUMXDUUROLUWJVWKUVPJUVNVUBUWJVVNAGVVRWHYTVUBVWMVWDJUVNUWJ
      VUBVWLTVWCQVUBUWGUWIUSVWLTJUWGUWIUWFUWHUWEUWFUWEUWHCXFDXFUVAUVBUWGUWIVVPV
      VQUVCUVIUVDUVEUVFXKUVGUYTYAYQUVHUVJYEYBXSBCDAUVKUVLUVM $.
      $( [26-Sep-2013] $)
  $}

  ${
    $d f n x A $.  $d f B $.  $d f G $.  $d f n x R $.
    fz1iso.1 $e |- G = ( rec ( ( n e. _V |-> ( n + 1 ) ) , 1 ) |` om ) $.
    fz1iso.2 $e |- B = ( NN i^i ( `' < " { ( ( # ` A ) + 1 ) } ) ) $.
    fz1iso.3 $e |- C = ( om i^i ( `' G ` ( ( # ` A ) + 1 ) ) ) $.
    $( Lemma for ~ fz1iso . $)
    fz1isolem $p |- ( ( R Or A /\ A e. Fin ) ->
                  E. f f Isom < , R ( ( 1 ... ( # ` A ) ) , A ) ) $=
      ( wcel cep wiso con0 c1 cfv clt cn com cvv vx wor cfn wa cv wex chash cfz
      wrex co wreu wwe simpr wofi ordtype syl2anc reurex syl ccnv cres ccom cn0
      hashcl ad2antlr caddc cuz wceq nnuz om2uzisoi isoeq5 mpbiri ax-mp nn0p1nn
      1z isocnv cin csn cima fvex epini ineq2i eqtr4i isoini2 sylancr wb cle cz
      wbr nnz nn0z eluz syl2anr zleltp1 ovex vex eliniseg syl6bbr bitr2d eleq2i
      pm5.32da elin bitri elfzuzb anbi1i bitr4i 3bitr4g eqrdv isoeq4 mpbid cmpt
      cc0 crdg simprl simplr wf1o simprr isof1o f1oen 3syl enfi mpbird sylanbrc
      cen onfin2 syl6eleqr cmin eqid uzrdgxfr ax-1cn subid1i oveq2i syl6eq ccrd
      0z ensymg sylc cardennn fveq2d eqtr3d eqtrd hashgval oveq1d f1ocnvfv1 wss
      ineq2d word ordom ordelss sseqin2 sylib isotr wfun wfn rdgfnon fnfun omex
      syl5eq resfunexg mp2an eqeltri cnvex coex isoeq1 cla4ev exlimdv rexlimdva
      resex expr mpd ) ADUBZAUCKZUDZUAUEZALDFUEZMZFUFZUANUIZOAUGPZUHUJZAQDEUEZM
      ZEUFZUVLUVPUANUKZUVQUVLUVKADULUWCUVJUVKUMADUNUAAUCDFUOUPUVPUANUQURUVLUVPU
      WBUANUVLUVMNKZUDUVOUWBFUVLUWDUVOUWBUVLUWDUVOUDZUDZUVSAQDUVNGUSZBUTZVAZMZU
      WBUWFUVSUVMQLUWHMZUVOUWJUWFUVSCQLUWHMZUWKUWFBCQLUWHMZUWLUWFUVRVBKZUWMUVKU
      WNUVJUWEAVCVDZUWNRSQLUWGMZUVROVEUJZRKUWMSRLQGMZUWPROVFPZVGZUWRVHUWTUWRSUW
      SLQGMFOGVNHVISRUWSLQGVJVKVLZSRLQGVOVLUVRVMRSBCQLUWGUWQICSUWQUWGPZVPZSLUSU
      XBVQVRZVPJUXDUXBSUXBUWQUWGVSVTWAWBWCWDURUWFBUVSVGUWMUWLWEUWFEBUVSUWFUVTRK
      ZUVTQUSUWQVQVRZKZUDZUXEUVRUVTVFPKZUDZUVTBKZUVTUVSKZUWFUXEUXGUXIUWFUXEUDZU
      XIUVTUVRWFWHZUXGUXEUVTWGKZUVRWGKZUXIUXNWEUWFUVTWIZUWFUWNUXPUWOUVRWJURZUVT
      UVRWKWLUXMUXNUVTUWQQWHZUXGUXEUXOUXPUXNUXSWEUWFUXQUXRUVTUVRWMWLUWQTKUXGUXS
      WEUVROVEWNQUWQUVTTEWOWPVLWQWRWTUXKUVTRUXFVPZKUXHBUXTUVTIWSUVTRUXFXAXBUXLU
      VTUWSKZUXIUDZUXJUVRTKUXLUYBWEAUGVSTUVTOUVRXCVLUXEUYAUXIRUWSUVTVHWSXDXEXFX
      GBCUVSQLUWHXHURXIUWFCUVMVGUWLUWKWEUWFCUXCUVMJUWFUXCSUVMVPZUVMUWFUXBUVMSUW
      FUVMGPZUWGPZUXBUVMUWFUYDUWQUWGUWFUYDUVMFTUVNOVEUJXJZXKXLSUTZPZOVEUJZUWQUW
      FUVMSKZUYDUYIVGUWFUVMNUCVPZSUWFUWDUVMUCKZUVMUYKKUVLUWDUVOXMUWFUYLUVKUVJUV
      KUWEXNZUWFUVKUVMAYCWHZUYLUVKWEUYMUWFUVOUVMAUVNXOUYNUVLUWDUVOXPZUVMALDUVNX
      QUVMAUVNUAWOXRXSZUVMAUCXTUPYAUVMNUCXAYBYDYEZUYJUYDUYHOXKYFUJZVEUJUYIFOXKG
      UYGUVMHUYGYGZVNYNYHUYROUYHVEOYIYJYKYLURUWFUYHUVROVEUWFAYMPZUYGPZUYHUVRUWF
      UYTUVMUYGUWFAUVMYCWHZUYJUYTUVMVGUWFUVKUYNVUBUYMUYPUVMAUCYOYPUYQAUVMYQUPYR
      UVKVUAUVRVGUVJUWEFAUYGUYSUUAVDYSUUBYTYRUWFSRGXOZUYJUYEUVMVGUWRVUCUXASRLQG
      XQVLUYQSRUVMGUUCWDYSUUEUWFUVMSUUDZUYCUVMVGUWFSUUFUYJVUDUUGUYQSUVMUUHWDUVM
      SUUIUUJYTUUQUVSCUVMQLUWHVJURXIUYOUVSUVMAQLDUVNUWHUUKUPUWAUWJEUWIUVNUWHFWO
      UWGBGGUYFOXLZSUTZTHVUEUULZSTKVUFTKVUENUUMVUGOUYFUUNNVUEUUOVLUUPVUESTUURUU
      SUUTUVAUVGUVBUVSAQDUWIUVTUVCUVDURUVHUVEUVFUVI $.
      $( [2-Apr-2014] $)
  $}

  ${
    $d f n A $.  $d f n R $.
    $( Any finite ordered set has an order isometry to a one-based finite
       sequence.  (Contributed by Mario Carneiro, 2-Apr-2014.) $)
    fz1iso $p |- ( ( R Or A /\ A e. Fin ) ->
                  E. f f Isom < , R ( ( 1 ... ( # ` A ) ) , A ) ) $=
      ( vn cn clt ccnv chash cfv c1 caddc co csn cima cin com cvv cv cmpt eqid
      crdg cres fz1isolem ) AEFGAHIJKLZMNOZPUDDQDRJKLSJUAPUBZGIOZBCDUFUFTUETUGT
      UC $.
      $( [2-Apr-2014] $)
  $}

  ${
    $d k m n y A $.  $d k m n y F $.  $d k m n y G $.  $d m n y H $.  $d y N $.
    $d k m n y M $.  $d k m n y P $.  $d k m n y ph $.  $d k n S $.  $d k Z $.
    seqcoll.1 $e |- ( ( ph /\ k e. S ) -> ( Z P k ) = k ) $.
    seqcoll.1b $e |- ( ( ph /\ k e. S ) -> ( k P Z ) = k ) $.
    seqcoll.c $e |- ( ( ph /\ ( k e. S /\ n e. S ) ) -> ( k P n ) e. S ) $.
    seqcoll.a $e |- ( ph -> Z e. S ) $.
    seqcoll.2 $e |- ( ph -> G Isom < , < ( ( 1 ... ( # ` A ) ) , A ) ) $.
    seqcoll.3 $e |- ( ph -> N e. ( 1 ... ( # ` A ) ) ) $.
    seqcoll.4 $e |- ( ph -> A C_ ( ZZ>= ` M ) ) $.
    seqcoll.5 $e |- ( ( ph /\ k e. ( M ... ( G ` ( # ` A ) ) ) ) ->
                      ( F ` k ) e. S ) $.
    seqcoll.6 $e |- ( ( ph /\ k e. ( ( M ... ( G ` ( # ` A ) ) ) \ A ) ) ->
                      ( F ` k ) = Z ) $.
    seqcoll.7 $e |- ( ( ph /\ n e. ( 1 ... ( # ` A ) ) ) ->
                      ( H ` n ) = ( F ` ( G ` n ) ) ) $.
    $( The function ` F ` contains a sparse set of non-zero values to be
       summed.  The function ` G ` is an order isomorphism from the set of
       non-zero values of ` F ` to a 1-based finite sequence, and ` H `
       collects these non-zero values together.  Under these conditions, the
       sum over the values in ` H ` yields the same result as the sum over the
       original set ` F ` .  (Contributed by Mario Carneiro, 2-Apr-2014.) $)
    seqcoll $p |- ( ph -> ( seq M ( P , F ) ` ( G ` N ) ) =
                          ( seq 1 ( P , H ) ` N ) ) $=
      ( vy vm c1 chash cfv cfz co wcel cseq wceq cn wi elfznn caddc eleq1 fveq2
      syl cv fveq2d eqeq12d imbi12d imbi2d cuz cres wf wf1o clt wiso isof1o cvv
      f1of fvex elfzuz2 sylancr eluzfz1 ffvelrn syl2anc sseldd cle eluzle wn wb
      wbr eluzfz2 isorel syl12anc 1re ltnle sylancl eluzelz zre 3bitr3d con4bid
      cr cz mpbid mpbird expcom vtoclga mpcom cmin wss peano2zm ltm1 ltle letrd
      wa mpd eluz fzss2 ssel2 sylan 3syl nnre adantr breq1d mtbid ex con2d syl5
      imp sylanbrc seq1 ax-mp ad2antlr elfzuz3 elfzuzb ad2antrr zltlem1 elfzle2
      syldan adantl elfzelz uztrn adantrr 3bitrd cc a2d eluzelre elfz5 cdif w3a
      eleq1d eluzel2 simp3 ccnv f1ocnv nnge1 lenlt f1ocnvfv2 bitrd sylbid eldif
      elfzm11 seqid fveq1d uzid fvres eqtr4d 3eqtr3d 1z syl6eqr a1d simplr nnuz
      syl6eleq simpr peano2uzr imim1d oveq1 simpll ltp1 simplll elfzuz peano2uz
      nnz syl2anr ovex mpan syl2an elfzle1 jca simprr btwnnz 3expib sylc breq2d
      seqcl zltp1le anbi12d expr seqid2 oveq1d impcom adantlr oveq2d zcn ax-1cn
      npcan eluzp1p1 eqeltrrd seqm1 3eqtr4rd seqp1 syl5ibr syld nnind ) AKUEBUF
      UGZUHUIZUJZKHUGZCGJUKZUGZKCIUEUKZUGZULZRKUMUJZAUXLUXRUNZAUXLUXSRKUXJUOUSA
      UCUTZUXKUJZUYAHUGZUXNUGZUYAUXPUGZULZUNZUNAUEUXKUJZUEHUGZUXNUGZUEUXPUGZULZ
      UNZUNAUDUTZUXKUJZUYNHUGZUXNUGZUYNUXPUGZULZUNZUNAUYNUEUPUIZUXKUJZVUAHUGZUX
      NUGZVUAUXPUGZULZUNZUNAUXTUNUCUDKUYAUEULZUYGUYMAVUHUYBUYHUYFUYLUYAUEUXKUQV
      UHUYDUYJUYEUYKVUHUYCUYIUXNUYAUEHURVAUYAUEUXPURVBVCVDUYAUYNULZUYGUYTAVUIUY
      BUYOUYFUYSUYAUYNUXKUQVUIUYDUYQUYEUYRVUIUYCUYPUXNUYAUYNHURVAUYAUYNUXPURVBV
      CVDUYAVUAULZUYGVUGAVUJUYBVUBUYFVUFUYAVUAUXKUQVUJUYDVUDUYEVUEVUJUYCVUCUXNU
      YAVUAHURVAUYAVUAUXPURVBVCVDUYAKULZUYGUXTAVUKUYBUXLUYFUXRUYAKUXKUQVUKUYDUX
      OUYEUXQVUKUYCUXMUXNUYAKHURVAUYAKUXPURVBVCVDAUYLUYHAUYJUEIUGZUYKAUYIUXNUYI
      VEUGZVFZUGZUYICGUYIUKZUGZUYJVULAUYIVUNVUPAECDGJUYILMPABJVEUGZUYISAUXKBHVG
      ZUYHUYIBUJAUXKBHVHZVUSAUXKBVIVIHVJZVUTQUXKBVIVIHVKUSZUXKBHVMUSZAUXJUEVEUG
      ZUJZUYHAUXJVLUJZUXLVVEBUFVNZRVLKUEUXJVOVPZUEUXJVQUSZUXKBUEHVRVSVTZUYIJUXJ
      HUGZUHUIZUJZAUYIGUGZDUJZAVVMUYIVVKWAWEZAUEUXJWAWEZVVPAVVEVVQVVHUEUXJWBUSA
      VVQVVPAUXJUEVIWEZVVKUYIVIWEZVVQWCZVVPWCZAVVAUXJUXKUJZUYHVVRVVSWDQAVVEVWBV
      VHUEUXJWFUSZVVIUXKBUXJUEVIVIHWGWHAUXJWPUJZUEWPUJZVVRVVTWDAVVEVWDVVHUEUXJU
      UAUSZWIUXJUEWJWKAVVKWPUJZUYIWPUJZVVSVWAWDAVVKWQUJZVWGAVVKVURUJVWIABVURVVK
      SAVUSVWBVVKBUJVVCVWCUXKBUXJHVRVSVTJVVKWLUSZVVKWMUSZAUYIWQUJZVWHAUYIVURUJZ
      VWLVVJJUYIWLUSZUYIWMUSZVVKUYIWJVSWNWOWRZAVWMVWIVVMVVPWDVVJVWJUYIJVVKUUBVS
      WSAEUTZGUGZDUJZUNAVVOUNEUYIVVLVWQUYIULZVWSVVOAVWTVWRVVNDVWQUYIGURUUEVDAVW
      QVVLUJZVWSTWTXAXBAVWQJUYIUEXCUIZUHUIZUJZVWQVVLBUUCUJZVWRLULZAVXDXIVXAVWQB
      UJZWCZVXEAVXCVVLXDZVXDVXAAVVKVXBVEUGUJZVXIAVXJVXBVVKWAWEZAVXBUYIVVKAVXBWQ
      UJZVXBWPUJZAVWLVXLVWNUYIXEUSZVXBWMUSZVWOVWKAVXBUYIVIWEZVXBUYIWAWEZAVWHVXP
      VWOUYIXFUSAVXMVWHVXPVXQUNVXOVWOVXBUYIXGVSXJVWPXHAVXLVWIVXJVXKWDVXNVWJVXBV
      VKXKVSWSVXBJVVKXLUSVXCVVLVWQXMXNAVXDVXHAVXDVWQWQUJZJVWQWAWEZVWQUYIVIWEZUU
      DZVXHAJWQUJZVWLVXDVYAWDAVWMVYBVVJJUYIUUFUSZVWNVWQJUYIUUPVSVYAVXTAVXHVXRVX
      SVXTUUGAVXGVXTAVXGVXTWCAVXGXIZVWQHUUHZUGZUEVIWEZVXTVYDVYFUXKUJZVYFUMUJZVY
      GWCZABUXKVYEVGZVXGVYHAVUTBUXKVYEVHVYKVVBUXKBHUUIBUXKVYEVMXOZBUXKVWQVYEVRZ
      XNZVYFUXJUOVYIUEVYFWAWEZVYJVYFUUJVYIVWEVYFWPUJVYOVYJWDWIVYFXPUEVYFUUKVPWR
      XOVYDVYGVYFHUGZUYIVIWEZVXTVYDVVAVYHUYHVYGVYQWDAVVAVXGQXQVYNAUYHVXGVVIXQUX
      KBVYFUEVIVIHWGWHVYDVYPVWQUYIVIAVUTVXGVYPVWQULZVVBUXKBVWQHUULZXNXRUUMXSXTY
      AYBUUNYCVWQVVLBUUOZYDUAYMUUQUURAUYIVUMUJZVUOUYJULAVWLWUAVWNUYIUUSUSUYIVUM
      UXNUUTUSAVUQVVNVULAVWLVUQVVNULVWNCGUYIYEUSUYHAVULVVNULZVVIAFUTZIUGZWUCHUG
      ZGUGZULZUNZAWUBUNFUEUXKWUCUEULZWUGWUBAWUIWUDVULWUFVVNWUCUEIURWUIWUEUYIGWU
      CUEHURVAVBVDAWUCUXKUJWUGUBWTZXAXBUVAUVBUEWQUJUYKVULULUVCCIUEYEYFUVDUVEUYN
      UMUJZAUYTVUGAWUKUYTVUGUNAWUKXIZUYTVUBUYSUNVUGWULVUBUYOUYSWULVUBUYOWULVUBX
      IZUYNVVDUJZUXJUYNVEUGUJZUYOWUMUYNUMVVDAWUKVUBUVFUVGUVHZWUMUYNWQUJZUXJVUAV
      EUGUJZWUOWUKWUQAVUBUYNUVRZYGWUMVVFVUBWURVVGWULVUBUVIZVLVUAUEUXJYHVPUYNUXJ
      UVJVSVVFUYOWUNWUOXIWDVVGVLUYNUEUXJYIYFYDZXTUVKWULVUBUYSVUFWULVUBUYSVUFUNU
      YSVUFWUMUYQVUAIUGZCUIZUYRWVBCUIZULUYQUYRWVBCUVLWUMVUDWVCVUEWVDWUMUYQVUCGU
      GZCUIVUCUEXCUIZUXNUGZWVECUIZWVCVUDWUMUYQWVGWVECWUMECDGUYPJWVFLWUMAVWQDUJZ
      VWQLCUIVWQULAWUKVUBUVMZNXNWUMBVURUYPABVURXDZWUKVUBSYJZWUMVUSUYOUYPBUJAVUS
      WUKVUBVVCYJZWVAUXKBUYNHVRVSVTZWUMWVFUYPVEUGZUJZUYPWVFWAWEZWUMUYPVUCVIWEZW
      VQWUMUYNVUAVIWEZWVRWUMUYNWPUJZWVSWUKWVTAVUBUYNXPYGUYNUVNUSWUMVVAUYOVUBWVS
      WVRWDAVVAWUKVUBQYJZWVAWUTUXKBUYNVUAVIVIHWGWHWRWUMUYPWQUJZVUCWQUJZWVRWVQWD
      WUMUYPVURUJZWWBWVNJUYPWLUSZWUMVUCVURUJWWCWUMBVURVUCWVLWUMVUSVUBVUCBUJWVMW
      UTUXKBVUAHVRVSVTJVUCWLUSZUYPVUCYKVSWRWUMWWBWVFWQUJZWVPWVQWDWWEWUMWWCWWGWW
      FVUCXEUSZUYPWVFXKVSWSZWUMEFCDGJUYPWVNWUMVWQJUYPUHUIZUJZVXAVWSWUMWWJVVLXDZ
      WWKVXAWUMVVKWVOUJZWWLWUMVVKWVFVEUGUJZWVPWWMWUMWWNWVFVVKWAWEZWUMWVFVUCVVKW
      UMWWGWVFWPUJZWWHWVFWMUSZWUMWWCVUCWPUJZWWFVUCWMUSZAVWGWUKVUBVWKYJZWUMWVFVU
      CVIWEZWVFVUCWAWEZWUMWWRWXAWWSVUCXFUSWUMWWPWWRWXAWXBUNWWQWWSWVFVUCXGVSXJWU
      MVUAUXJWAWEZVUCVVKWAWEZVUBWXCWULVUAUEUXJYLYNWUMWXCWXDWUMUXJVUAVIWEZVVKVUC
      VIWEZWXCWCZWXDWCZWUMVVAVWBVUBWXEWXFWDWWAAVWBWUKVUBVWCYJWUTUXKBUXJVUAVIVIH
      WGWHWUMVWDVUAWPUJZWXEWXGWDAVWDWUKVUBVWFYJWUMVUAWQUJZWXIVUBWXJWULVUAUEUXJY
      OYNVUAWMUSUXJVUAWJVSWUMVWGWWRWXFWXHWDWWTWWSVVKVUCWJVSWNWOWRXHWUMWWGVWIWWN
      WWOWDWWHAVWIWUKVUBVWJYJWVFVVKXKVSWSZWWIWVFVVKUYPYPVSUYPJVVKXLUSWWJVVLVWQX
      MXNWUMAVXAVWSWVJTXNYMWUMAWVIWUCDUJXIVWQWUCCUIDUJWVJOXNUWJWUMVWQUYPUEUPUIZ
      WVFUHUIUJZXIZAVXEVXFAWUKVUBWXMUVOWXNVXAVXHVXEWXNVWQVURUJZVVKVWQVEUGZUJZVX
      AWXMVWQWXLVEUGUJWXLVURUJZWXOWUMVWQWXLWVFUVPWUMWWDWXRWVNJUYPUVQUSWXLVWQJYP
      UVSWUMWWNWVFWXPUJZWXQWXMWXKWVFVLUJWXMWXSVUCUEXCUVTVLVWQWXLWVFYHUWAWVFVVKV
      WQYPUWBVVKVLUJVXAWXOWXQXIWDUXJHVNVLVWQJVVKYIYFYDWUMWXMVXHWXMWXLVWQWAWEZVW
      QWVFWAWEZXIZWUMVXHWXMWXTWYAVWQWXLWVFUWCVWQWXLWVFYLUWDWUMVXGWYBWULVUBVXGWY
      BWCWULVUBVXGXIZXIZUYNVYFVIWEZVYFVUAVIWEZXIZWYBWYDWUQVYFWQUJZWYGWCWUKWUQAW
      YCWUSYGWYDVYHWYHWYDVYKVXGVYHAVYKWUKWYCVYLYJWULVUBVXGUWEZVYMVSZVYFUEUXJYOU
      SWUQWYGWYHWUQWYEWYFWYHWCUYNVYFUWFUWGYAUWHWYDWYEWXTWYFWYAWYDWYEUYPVYPVIWEZ
      UYPVWQVIWEZWXTWYDVVAUYOVYHWYEWYKWDAVVAWUKWYCQYJZWULVUBUYOVXGWVAYQWYJUXKBU
      YNVYFVIVIHWGWHWYDVYPVWQUYPVIWYDVUTVXGVYRAVUTWUKWYCVVBYJWYIVYSVSZUWIWYDWWB
      VXRWYLWXTWDWULVUBWWBVXGWWEYQWYDWXOVXRWYDBVURVWQAWVKWUKWYCSYJWYIVTJVWQWLUS
      ZUYPVWQUWKVSYRWYDWYFVYPVUCVIWEZVWQVUCVIWEZWYAWYDVVAVYHVUBWYFWYPWDWYMWYJWU
      LVUBVUBVXGWUTYQUXKBVYFVUAVIVIHWGWHWYDVYPVWQVUCVIWYNXRWYDVXRWWCWYQWYAWDWYO
      WULVUBWWCVXGWWFYQVWQVUCYKVSYRUWLXSUWMYAYBYCVYTYDUAVSUWNUWOWUMWVBWVEUYQCAV
      UBWVBWVEULZWUKVUBAWYRWUHAWYRUNFVUAUXKWUCVUAULZWUGWYRAWYSWUDWVBWUFWVEWUCVU
      AIURWYSWUEVUCGWUCVUAHURVAVBVDWUJXAUWPUWQUWRWUMVYBVUCJUEUPUIVEUGZUJVUDWVHU
      LAVYBWUKVUBVYCYJWUMWVFUEUPUIZVUCWYTWUMVUCYSUJZUEYSUJXUAVUCULWUMWWCXUBWWFV
      UCUWSUSUWTVUCUEUXAWKWUMWVFVURUJZXUAWYTUJWUMWVPWWDXUCWWIWVNUYPWVFJYPVSJWVF
      UXBUSUXCCGJVUCUXDVSUXEWUMWUNVUEWVDULWUPCIUEUYNUXFUSVBUXGXTYTUXHWTYTUXIXBX
      J $.
      $( [2-Apr-2014] $)
  $}


