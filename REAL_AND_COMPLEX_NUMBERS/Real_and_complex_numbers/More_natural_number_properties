$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        More natural number properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    nnsqcl.1 $e |- N e. NN $.
    $( The square of a natural number is a natural number. $)
    nnsqcli $p |- ( N ^ 2 ) e. NN $=
      ( cn wcel c2 cn0 cexp co 2nn0 nnexpcl mp2an ) ACDEFDAEGHCDBIAEJK $.
      $( [20-Aug-2001] $)

    $( A natural number is less than or equal to its square. $)
    nnlesqi $p |- N <_ ( N ^ 2 ) $=
      ( c1 cmul co c2 cexp cle nncni mulid1i wbr cn wcel nnge1 ax-mp cc0 clt wb
      nngt0i 1re nnrei lemul2i mpbi sqvali breqtrri eqbrtrri ) ACDEZAAFGEZHAABI
      ZJUGAADEZUHHCAHKZUGUJHKZALMUKBANOPAQKUKULRABSCAATABUAZUMUBOUCAUIUDUEUF $.
      $( [15-Sep-1999] $)

    $( A natural number is even iff its square is even. $)
    nnesqi $p |- ( ( N / 2 ) e. NN <-> ( ( N ^ 2 ) / 2 ) e. NN ) $=
      ( c2 cdiv co cn wcel cexp cmul nnmulcl 2cn 2ne0 oveq1i eqtr3i mpan oveq2i
      c1 caddc cc0 clt anidms nncni divcli mulassi sqvali divcan2i 3eqtr4ri 2nn
      divassi syl5eqelr syl wn ax-1cn addcli binom2i mulid1i sq1 oveq12i mulcli
      nnsqcli add32i 3eqtri nnrei readdcli divdiri divcan3i 3eqtr3i eleq1i cmin
      1re recni 2re redivcli addsubassi subidi addid1i wbr nngt0i lt01 addgt0ii
      addid2i 2pos divgt0ii 0re ltadd1i mpbi eqbrtrri wb nnsub mpbii sylbi 3syl
      con3i nneoi 3imtr4i impbii ) ACDEZFGZACHEZCDEZFGZWRWQWQIEZFGZXAWRXCWQWQJU
      AXCWTCXBIEZFCWQIEZWQIEZXDWTCWQWQKACABUBZKLUCZXHUDAAIEZCDEAWQIEWTXFAACXGXG
      KLUIWSXICDAXGUEMXEAWQIACXGKLUFMUGNCFGZXCXDFGUHCXBJOUJUKWSQREZCDEZFGZULAQR
      EZCDEZFGZULXAWRXPXMXPXOXOIEZFGZCXQIEZFGZXMXPXRXOXOJUAXJXRXTUHCXQJOXTXLARE
      ZFGZXMXSYAFCXOIEZXOIEXNXOIEZXSYAYCXNXOIXNCAQXGUMUNZKLUFMCXOXOKXNCYEKLUCZY
      FUDXNXNIEZCDEZYDYAXNXNCYEYEKLUIYHXKCAIEZREZCDEXLYICDEZREYAYGYJCDXNCHEZYGY
      JXNYEUEYLWSCAQIEZIEZREZQCHEZREWSYIREZQREYJAQXGUMUOYOYQYPQRYNYIWSRYMACIAXG
      UPPPUQURWSYIQWSABUTZUBCAKXGUSZUMVAVBNMXKYICXKWSQWSYRVCZVJVDZVKYSKLVEYKAXL
      RACXGKLVFPVBNVGVHYBXLYAAVIEZFUUBXLAAVIEZREXLSREXLXLAAXLXKCUUAVLLVMZVKZXGX
      GVNUUCSXLRAXGVOPXLUUEVPVBYBAYATVQZUUBFGZSAREZAYATAXGWASXLTVQUUHYATVQXKCUU
      AVLWSQYTVJWSYRVRVSVTWBWCSXLAWDUUDABVCWEWFWGAFGYBUUFUUGWHBAYAWIOWJUJWKWLWM
      WSYRWNABWNWOWP $.
      $( [20-Aug-2001] $)
  $}

