$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real and imaginary parts; conjugate
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Re $. $( Real part of a complex number. $)
  $c Im $. $( Imaginary part of a complex number. $)
  $c * $. $( Complex conjugate function. $)

  $( Extend class notation to include real part of a complex number. $)
  cre $a class Re $.

  $( Extend class notation to include imaginary part of a complex number. $)
  cim $a class Im $.

  $( Extend class notation to include complex conjugate function. $)
  ccj $a class * $.

  ${
    $d x y $.
    $( Define the complex conjugate function.  See ~ cjcli for its closure and
       ~ cjval for its value. $)
    df-cj $a |- * = ( x e. CC |-> ( iota_ y e. CC
                 ( ( x + y ) e. RR /\ ( _i x. ( x - y ) ) e. RR ) ) ) $.

    $( Define a function whose value is the real part of a complex number.  See
       ~ reval for its value, ~ recli for its closure, and ~ replim for its use
       in decomposing a complex number. $)
    df-re $a |- Re = ( x e. CC |-> ( ( x + ( * ` x ) ) / 2 ) ) $.

    $( Define a function whose value is the imaginary part of a complex
       number.  See ~ imval for its value, ~ imcli for its closure, and
       ~ replim for its use in decomposing a complex number. $)
    df-im $a |- Im = ( x e. CC |-> ( Re ` ( x / _i ) ) ) $.
  $}

  ${
    $d x y A $.
    $( The value of the conjugate of a complex number. $)
    cjval $p |- ( A e. CC -> ( * ` A ) = ( iota_ x e. CC
          ( ( A + x ) e. RR /\ ( _i x. ( A - x ) ) e. RR ) ) ) $=
      ( vy cv caddc co cr wcel ci cmin cmul wa cc crio wceq oveq1 eleq1d oveq2d
      ccj anbi12d riotabidv df-cj riotaex fvmpt ) CBCDZADZEFZGHZIUEUFJFZKFZGHZL
      ZAMNBUFEFZGHZIBUFJFZKFZGHZLZAMNMSUEBOZULURAMUSUHUNUKUQUSUGUMGUEBUFEPQUSUJ
      UPGUSUIUOIKUEBUFJPRQTUACAUBURAMUCUD $.
      $( [6-Nov-2013] $)

    $( The defining property of the complex conjugate. $)
    cjth $p |- ( A e. CC ->
          ( ( A + ( * ` A ) ) e. RR /\ ( _i x. ( A - ( * ` A ) ) ) e. RR ) ) $=
      ( vx cc wcel cv caddc co cr ci cmin cmul ccj cfv wsbc crio syl wceq oveq2
      wa eleq1d wreu cju riota4 wb cjval dfsbcq mpbird fvex anbi12d sbcie sylib
      oveq2d ) ACDZABEZFGZHDZIAUNJGZKGZHDZSZBALMZNZAVAFGZHDZIAVAJGZKGZHDZSZUMVB
      UTBUTBCOZNZUMUTBCUAVJBAUBUTBCUCPUMVAVIQVBVJUDBAUEUTBVAVIUFPUGUTVHBVAALUHU
      NVAQZUPVDUSVGVKUOVCHUNVAAFRTVKURVFHVKUQVEIKUNVAAJRULTUIUJUK $.
      $( [6-Nov-2013] $)

    $( Domain and codomain of the conjugate function. $)
    cjf $p |- * : CC --> CC $=
      ( vx vy cv caddc co cr wcel ci cmin cmul wa cc crio wral ccj wreu riotacl
      wf cju syl rgen df-cj fmpt mpbi ) ACZBCZDEFGHUEUFIEJEFGKZBLMZLGZALNLLORUI
      ALUELGUGBLPUIBUESUGBLQTUAALLUHOABUBUCUD $.
      $( [6-Nov-2013] $)

    $( The conjugate of a complex number is a complex number (closure law). $)
    cjcl $p |- ( A e. CC -> ( * ` A ) e. CC ) $=
      ( cc ccj cjf ffvelrni ) BBACDE $.
      $( [6-Nov-2013] $) $( [10-May-1999] $)

    $( The value of the real part of a complex number. $)
    reval $p |- ( A e. CC -> ( Re ` A ) = ( ( A + ( * ` A ) ) / 2 ) ) $=
      ( vx cv ccj cfv caddc co c2 cdiv cre wceq fveq2 oveq12 mpdan oveq1d df-re
      cc ovex fvmpt ) BABCZTDEZFGZHIGAADEZFGZHIGQJTAKZUBUDHIUEUAUCKUBUDKTADLTAU
      AUCFMNOBPUDHIRS $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The value of the imaginary part of a complex number. $)
    imval $p |- ( A e. CC -> ( Im ` A ) = ( Re ` ( A / _i ) ) ) $=
      ( vx cv ci cdiv co cre cfv cc cim wceq oveq1 fveq2d df-im fvex fvmpt ) BA
      BCZDEFZGHADEFZGHIJQAKRSGQADELMBNSGOP $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number in terms of the real part
       function. $)
    imre $p |- ( A e. CC -> ( Im ` A ) = ( Re ` ( -u _i x. A ) ) ) $=
      ( cc wcel cim cfv ci cdiv co cre cneg cmul imval cc0 wne wceq ax-icn ine0
      c1 divrec2 mp3an23 irec oveq1i syl6eq fveq2d eqtrd ) ABCZADEAFGHZIEFJZAKH
      ZIEALUFUGUIIUFUGRFGHZAKHZUIUFFBCFMNUGUKOPQAFSTUJUHAKUAUBUCUDUE $.
      $( [6-Nov-2013] $) $( [12-May-2005] $)

    $( The real part of a complex number is real. $)
    recl $p |- ( A e. CC -> ( Re ` A ) e. RR ) $=
      ( cc wcel cre cfv ccj caddc co c2 cdiv cr reval cmin cmul simpld rehalfcl
      ci cjth syl eqeltrd ) ABCZADEAAFEZGHZIJHZKALUAUCKCZUDKCUAUEQAUBMHNHKCAROU
      CPST $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number is real. $)
    imcl $p |- ( A e. CC -> ( Im ` A ) e. RR ) $=
      ( cc wcel cim cfv ci cneg cmul co cre cr imre ax-icn negcli ax-mulcl mpan
      recl syl eqeltrd ) ABCZADEFGZAHIZJEZKALTUBBCZUCKCUABCTUDFMNUAAOPUBQRS $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( Domain and codomain of the real part function.  (Contributed by Paul
       Chapman, 22-Oct-2007.) $)
    ref $p |- Re : CC --> RR $=
      ( vx cv ccj cfv caddc co c2 cdiv cr wcel cc wral reval recl eqeltrrd rgen
      cre wf df-re fmpt mpbi ) ABZUBCDEFGHFZIJZAKLKIQRUDAKUBKJUBQDUCIUBMUBNOPAK
      IUCQASTUA $.
      $( [6-Nov-2013] $) $( [22-Oct-2007] $)

    $( Domain and codomain of the imaginary part function.  (Contributed by
       Paul Chapman, 22-Oct-2007.) $)
    imf $p |- Im : CC --> RR $=
      ( vx cv ci cdiv co cre cfv cr wcel cc wral imval imcl eqeltrrd rgen df-im
      cim wf fmpt mpbi ) ABZCDEFGZHIZAJKJHQRUCAJUAJIUAQGUBHUALUAMNOAJHUBQAPST
      $.
      $( [6-Nov-2013] $) $( [22-Oct-2007] $)

    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crre $p |- ( ( A e. RR /\ B e. RR ) -> ( Re ` ( A + ( _i x. B ) ) ) = A )
        $=
      ( cr wcel ci cmul co caddc c2 cdiv cc wceq ax-icn sylancr syl syl2anc a1i
      cmin syl3anc oveq1d wa cre cfv ccj recn mulcl addcl syl2an reval cc0 recl
      eqeltrrd simpl resubcl adantl subcl halfcl adantr ax-mulcl pnpcan pnpcan2
      cjcl subdi eqtr4d ax-addcl addsub subsub 3eqtr4d 2times wne 2cn divsubdir
      oveq2d 2ne0 syl112anc 3eqtr3d divcan3 ax-mulass divass oveq12d c1 ixi 1re
      cneg renegcli eqeltri simpr ax-mulrcl cjth rehalfcl 3syl eqeltrd rimul wb
      simprd subeq0 mpbid eqtrd ) ACDZBCDZUAZAEBFGZHGZUBUCZXCXCUDUCZHGZIJGZAXAX
      CKDZXDXGLWSAKDZXBKDZXHWTAUEZWTEKDZBKDZXJMBUEZEBUFZNAXBUGUHZXCUIOZXAXGARGZ
      UJLZXGALZXAXRCDZEXRFGZCDXSXAXGCDWSYAXAXDXGCXQXAXHXDCDXPXCUKOULWSWTUMXGAUN
      PXAYBEEFGZBFGZEXCXERGZFGZIJGZRGZCXAEXBYEIJGZRGZFGZEXBFGZEYIFGZRGZYBYHXAXL
      XJYIKDZYKYNLXLXAMQZXAXLXMXJYPWTXMWSXNUOZXOPXAYEKDZYOXAXHXEKDZYRXPXAXHYSXP
      XCVBOZXCXEUPPZYEUQOEXBYIVCSXAXRYJEFXAXGIAFGZIJGZRGZIXBFGZIJGZYIRGZXRYJXAX
      FUUBRGZIJGZUUEYERGZIJGZUUDUUGXAUUHUUJIJXAXFAAHGZRGZXBXBHGZYERGZUUHUUJXAXC
      UULRGZXEHGZUUNXCRGZXEHGZUUMUUOXAUUPUURXEHXAUUPXBARGZUURXAXIXJXIUUPUUTLWSX
      IWTXKURZXAXLXMXJMYQEBUSNZUVAAXBAUTSXAXJXIXJUURUUTLUVBUVAUVBXBAXBVASVDTXAX
      HYSUULKDZUUMUUQLXPYTXAXIXIUVCUVAUVAAAVEPXCXEUULVFSXAUUNKDZXHYSUUOUUSLXAXJ
      XJUVDUVBUVBXBXBVEPXPYTUUNXCXEVGSVHXAUUBUULXFRXAXIUUBUULLUVAAVIOVMXAUUEUUN
      YERXAXJUUEUUNLUVBXBVIOTVHTXAXFKDZUUBKDZIKDZIUJVJZUUIUUDLXAXHYSUVEXPYTXCXE
      VEPZXAUVGXIUVFVKUVAIAUSNUVGXAVKQZUVHXAVNQZXFUUBIVLVOXAUUEKDZYRUVGUVHUUKUU
      GLXAUVGXJUVLVKUVBIXBUSNUUAUVJUVKUUEYEIVLVOVPXAUUCAXGRXAXIUVGUVHUUCALUVAUV
      JUVKAIVQSVMXAUUFXBYIRXAXJUVGUVHUUFXBLUVBUVJUVKXBIVQSTVPVMXAYDYLYGYMRXAXLX
      LXMYDYLLYPYPYQEEBVRSXAXLYRUVGUVHYGYMLYPUUAUVJUVKEYEIVSVOVTVHXAYDCDZYGCDZY
      HCDXAYCCDWTUVMYCWAWDCWBWAWCWEWFWSWTWGYCBWHNXAXHYFCDZUVNXPXHXFCDUVOXCWIWOY
      FWJWKYDYGUNPWLXRWMPXAXGKDZXIXSXTWNXAUVEUVPUVIXFUQOUVAXGAWPPWQWR $.
      $( [7-Nov-2013] $) $( [12-May-2005] $)

    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crim $p |- ( ( A e. RR /\ B e. RR ) -> ( Im ` ( A + ( _i x. B ) ) ) = B )
        $=
      ( cr wcel wa ci cmul co caddc cfv cdiv cre cneg wceq recn ax-icn ax-mulcl
      cc syl2an ine0 cim sylancr ax-addcl imval syl mpan cc0 wne divdir mpanr12
      3expa sylan2 c1 divrec2 mp3an23 oveq1i a1i mulneg12 3eqtrd divcan3 oveq12
      irec negcl addcom sylan 3eqtrrd fveq2d id renegcl crre syl2anr 3eqtr2d )
      ACDZBCDZEZAFBGHZIHZUAJZVQFKHZLJZBFAMZGHZIHZLJZBVOVQRDZVRVTNVMARDZVPRDZWEV
      NAOZVNFRDZBRDZWGPBOZFBQZUBAVPUCSVQUDUEVOWCVSLVMWFWJWCVSNVNWHWKWFWJEVSAFKH
      ZVPFKHZIHZWBBIHZWCWJWFWGVSWONZWIWJWGPWLUFWFWGEWIFUGUHZWQPTWFWGWIWREWQAVPF
      UIUKUJULWFWMWBNWNBNZWOWPNWJWFWMUMFKHZAGHZFMZAGHZWBWFWIWRWMXANPTAFUNUOXAXC
      NWFWTXBAGVBUPUQWIWFXCWBNPFAURUFUSWJWIWRWSPTBFUTUOWMWBWNBIVASWFWBRDZWJWPWC
      NWFWIWARDXDPAVCFWAQUBWBBVDVEVFSVGVNVNWACDWDBNVMVNVHAVIBWAVJVKVL $.
      $( [7-Nov-2013] $) $( [12-May-2005] $)

    $( Reconstruct a complex number from its real and imaginary parts. $)
    replim $p |- ( A e. CC -> A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cc wcel cv ci cmul co caddc wceq wrex cre cfv cim ax-cnre oveq12d
      cr oveq2d fveq2 wa crre crim eqcomd id eqeq12d syl5ibrcom rexlimivv syl )
      ADEABFZGCFZHIZJIZKZCRLBRLAAMNZGAONZHIZJIZKZBCAPUNUSBCRRUJREUKREUAZUSUNUMU
      MMNZGUMONZHIZJIZKUTVDUMUTVAUJVCULJUJUKUBUTVBUKGHUJUKUCSQUDUNAUMURVDUNUEUN
      UOVAUQVCJAUMMTUNUPVBGHAUMOTSQUFUGUHUI $.
      $( [7-Nov-2013] $) $( [10-May-1999] $)

    $( Value of the conjugate of a complex number.  The value is the real part
       minus ` _i ` times the imaginary part.  Definition 10-3.2 of [Gleason]
       p. 132. $)
    remim $p |- ( A e. CC ->
                      ( * ` A ) = ( ( Re ` A ) - ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cc wcel cfv cv caddc co cr ci cmin cmul wceq recnd ax-icn syl3anc
      wa eqtrd syl2anc ccj crio cre cim cjval replim recl imcl ax-mulcl sylancr
      oveq1d ppncan ax-addrcl eqeltrd pnncan ax-distr 3eqtr4d ax-mulass mp3an12
      a1i oveq2d syl eqcomd c1 cneg ixi 1re renegcli eqeltri ax-mulrcl jca wreu
      wb subcl cju ax-17 a17d oveq2 eleq1d anbi12d riota2f mpbid ) ADEZAUAFABGZ
      HIZJEZKAWDLIZMIZJEZRZBDUBZAUCFZKAUDFZMIZLIZBAUEWCAWOHIZJEZKAWOLIZMIZJEZRZ
      WKWONZWCWQWTWCWPWLWLHIZJWCWPWLWNHIZWOHIZXCWCAXDWOHAUFZUKWCWLDEZWNDEZXGXEX
      CNWCWLAUGZOZWCKDEZWMDEZXHPWCWMAUHZOZKWMUIUJZXJWLWNWLULQSWCWLJEZXPXCJEXIXI
      WLWLUMTUNWCWSKKMIZWMWMHIZMIZJWCWSKKXRMIZMIZXSWCWRXTKMWCXDWOLIZWNWNHIZWRXT
      WCXGXHXHYBYCNXJXOXOWLWNWNUOQWCAXDWOLXFUKWCXKXLXLXTYCNXKWCPUTXNXNKWMWMUPQU
      QVAWCXSYAWCXRDEZXSYANZWCXRWCWMJEZYFXRJEZXMXMWMWMUMTZOXKXKYDYEPPKKXRURUSVB
      VCSWCXQJEYGXSJEXQVDVEJVFVDVGVHVIYHXQXRVJUJUNVKWCWODEZWJBDVLXAXBVMWCXGXHYI
      XJXOWLWNVNTBAVOWJXABCDWOCGWOEBVPYIXABVQWDWONZWFWQWIWTYJWEWPJWDWOAHVRVSYJW
      HWSJYJWGWRKMWDWOALVRVAVSVTWATWBS $.
      $( [7-Nov-2013] $) $( [10-May-1999] $)
  $}

  ${
    recl.1 $e |- A e. CC $.
    $( The real part of a complex number is real (closure law). $)
    recli $p |- ( Re ` A ) e. RR $=
      ( cc wcel cre cfv cr recl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( The imaginary part of a complex number is real (closure law). $)
    imcli $p |- ( Im ` A ) e. RR $=
      ( cc wcel cim cfv cr imcl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( Closure law for complex conjugate. $)
    cjcli $p |- ( * ` A ) e. CC $=
      ( cc wcel ccj cfv cjcl ax-mp ) ACDAEFCDBAGH $.
      $( [11-May-1999] $)

    $( Construct a complex number from its real and imaginary parts. $)
    replimi $p |- A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) $=
      ( cc wcel cre cfv ci cim cmul co caddc wceq replim ax-mp ) ACDAAEFGAHFIJK
      JLBAMN $.
      $( [11-Mar-2008] $) $( [1-Oct-1999] $)
  $}

  ${
    crre.1 $e |- A e. RR $.
    crre.2 $e |- B e. RR $.
    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crrei $p |- ( Re ` ( A + ( _i x. B ) ) ) = A $=
      ( cr wcel ci cmul co caddc cre cfv wceq crre mp2an ) AEFBEFAGBHIJIKLAMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)

    $( The imaginary part of a complex number representation.  Definition
       10-3.1 of [Gleason] p. 132. $)
    crimi $p |- ( Im ` ( A + ( _i x. B ) ) ) = B $=
      ( cr wcel ci cmul co caddc cim cfv wceq crim mp2an ) AEFBEFAGBHIJIKLBMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)
  $}

  $( The imaginary part of a real number is 0. $)
  reim0 $p |- ( A e. RR -> ( Im ` A ) = 0 ) $=
    ( cr wcel ci cc0 cmul co caddc cim cc wceq recn ax-icn mul01i oveq2i addid1
    cfv syl5eq syl fveq2d 0re crim mpan2 eqtr3d ) ABCZADEFGZHGZIQZAIQEUEUGAIUEA
    JCZUGAKALUIUGAEHGAUFEAHDMNOAPRSTUEEBCUHEKUAAEUBUCUD $.
    $( [7-Nov-2013] $) $( [18-Mar-2005] $)

  $( A number is real iff its imaginary part is 0. $)
  reim0b $p |- ( A e. CC -> ( A e. RR <-> ( Im ` A ) = 0 ) ) $=
    ( cc wcel cr cim cfv cc0 wceq reim0 wa ci cmul co caddc replim adantr oveq2
    cre ax-icn mul01i syl6eq oveq2d recnd addid1 syl sylan9eqr eqtrd eqeltrd ex
    recl impbid2 ) ABCZADCZAEFZGHZAIULUOUMULUOJZAARFZDUPAUQKUNLMZNMZUQULAUSHUOA
    OPUOULUSUQGNMZUQUOURGUQNUOURKGLMGUNGKLQKSTUAUBULUQBCUTUQHULUQAUJZUCUQUDUEUF
    UGULUQDCUOVAPUHUIUK $.
    $( [24-Jul-2008] $) $( [26-Sep-2005] $)

  $( A number is real iff it equals its real part.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  rereb $p |- ( A e. CC -> ( A e. RR <-> ( Re ` A ) = A ) ) $=
    ( cc wcel cr cre cfv wceq wa ci cim co caddc cc0 replim adantr reim0 oveq2d
    cmul ax-icn mul01i syl6eq adantl recl recnd addid1 3eqtrrd eqeltrrd impbida
    syl simpr ) ABCZADCZAEFZAGZUKULHZAUMIAJFZRKZLKZUMMLKZUMUKAURGULANOUOUQMUMLU
    LUQMGUKULUQIMRKMULUPMIRAPQISTUAUBQUKUSUMGZULUKUMBCUTUKUMAUCZUDUMUEUIOUFUKUN
    HUMADUKUNUJUKUMDCUNVAOUGUH $.
    $( [20-Aug-2008] $)

  $( A product with a nonzero real multiplier is real iff the multiplicand is
     real. $)
  mulre $p |- ( ( A e. CC /\ B e. RR /\ B =/= 0 )
         -> ( A e. RR <-> ( B x. A ) e. RR ) ) $=
    ( cc wcel cr cre cfv wceq cmul co wb rereb 3ad2ant1 wa recnd syl3anc adantl
    ci caddc ax-icn cc0 wne w3a recl recn anim1i 3adant1 mulcan cim adantr imcl
    simp1 mulcl sylancr adddi replim oveq2d mul12 mp3an1 syl2an 3eqtr4d remulcl
    fveq2d sylan2 syl2anc eqtr2d eqeq1d sylan syl bitr4d ancoms 3adant3 3bitr2d
    crre ) ACDZBEDZBUAUBZUCZAEDZAFGZAHZBVTIJZBAIJZHZWCEDZVOVPVSWAKVQALMVRVTCDZV
    OBCDZVQNZWDWAKVOVPWFVQVOVTAUDZOZMVOVPVQULVPVQWHVOVPWGVQBUEZUFUGVTABUHPVOVPW
    DWEKZVQVPVOWLVPVONZWDWCFGZWCHZWEWMWBWNWCWMWNWBRBAUIGZIJZIJZSJZFGZWBWMWCWSFW
    MBVTRWPIJZSJZIJZWBBXAIJZSJZWCWSWMWGWFXACDZXCXEHVPWGVOWKUJVOWFVPWJQVOXFVPVOR
    CDZWPCDZXFTVOWPAUKZOZRWPUMUNQBVTXAUOPWMAXBBIVOAXBHVPAUPQUQWMWRXDWBSVPWGXHWR
    XDHZVOWKXJXGWGXHXKTRBWPURUSUTUQVAVCWMWBEDZWQEDZWTWBHVOVPVTEDXLWIBVTVBVDVOVP
    WPEDXMXIBWPVBVDWBWQVNVEVFVGWMWCCDZWEWOKVPWGVOXNWKBAUMVHWCLVIVJVKVLVM $.
    $( [21-Aug-2008] $)

  ${
    cjcji.1 $e |- A e. CC $.
    $( The conjugate of the conjugate is the original complex number.
       Proposition 10-3.4(e) of [Gleason] p. 133. $)
    cjcji $p |- ( * ` ( * ` A ) ) = A $=
      ( ccj cfv cre ci cmul co cmin cneg caddc recni ax-icn oveq2i cc wcel wceq
      cim remim ax-mp recli imcli mulcli negsubi mulneg2i 3eqtr4ri fveq2i crrei
      renegcli eqtri crimi 3eqtri oveq12i cjcli replimi subnegi eqtr4i 3eqtr4i
      ) ACDZEDZFUSRDZGHZIHZAEDZFARDZGHZJZIHZUSCDZAUTVDVBVGIUTVDFVEJZGHZKHZEDVDU
      SVLEVDVGKHVDVFIHZVLUSVDVFVDABUAZLZFVEMVEABUBZLZUCZUDVKVGVDKFVEMVQUEZNAOPU
      SVMQBASTUFZUGVDVJVNVEVPUIZUHUJVBFVLRDZGHVKVGVAWBFGUSVLRVTUGNWBVJFGVDVJVNW
      AUKNVSULUMUSOPVIVCQABUNUSSTAVDVFKHVHABUOVDVFVOVRUPUQUR $.
      $( [11-May-1999] $)

    $( A number is real iff its imaginary part is 0. $)
    reim0bi $p |- ( A e. RR <-> ( Im ` A ) = 0 ) $=
      ( cc wcel cr cim cfv cc0 wceq wb reim0b ax-mp ) ACDAEDAFGHIJBAKL $.
      $( [29-May-1999] $)

    $( A real number equals its real part.  Proposition 10-3.4(f) of [Gleason]
       p. 133. $)
    rerebi $p |- ( A e. RR <-> ( Re ` A ) = A ) $=
      ( cc wcel cr cre cfv wceq wb rereb ax-mp ) ACDAEDAFGAHIBAJK $.
      $( [24-Jul-2008] $) $( [27-Oct-1999] $)

    $( A number is real iff it equals its complex conjugate.  Proposition
       10-3.4(f) of [Gleason] p. 133. $)
    cjrebi $p |- ( A e. RR <-> ( * ` A ) = A ) $=
      ( cr wcel cim cfv cc0 wceq ccj reim0bi cneg imcli recni eqnegi ci cmul co
      ax-icn negcli caddc mulneg2i eqeq2i ine0 mulcani replimi cmin remim ax-mp
      cre cc mulcli negsubi eqtr4i eqeq12i addcani bitr2i 3bitr3i bitr3i 3bitri
      recli eqcom ) ACDAEFZGHZAAIFZHZVDAHABJVCVBVBKZHZVEVBVBABLMZNOVBPQZOVFPQZH
      VIVIKZHZVGVEVJVKVIOVBRVHUAUBVBVFOVHVBVHSRUCUDVEAUIFZVITQZVMVKTQZHVLAVNVDV
      OABUEVDVMVIUFQZVOAUJDVDVPHBAUGUHVMVIVMABUTMZOVBRVHUKZULUMUNVMVIVKVQVRVIVR
      SUOUPUQURAVDVAUS $.
      $( [11-Oct-1999] $)

    $( Real part of a complex conjugate. $)
    recji $p |- ( Re ` ( * ` A ) ) = ( Re ` A ) $=
      ( ccj cfv cre ci cim cneg cmul caddc cmin recli recni ax-icn imcli mulcli
      co negsubi mulneg2i oveq2i wcel wceq remim ax-mp 3eqtr4ri fveq2i renegcli
      cc crrei eqtri ) ACDZEDAEDZFAGDZHZIQZJQZEDULUKUPEULFUMIQZHZJQULUQKQZUPUKU
      LUQULABLZMFUMNUMABOZMZPRUOURULJFUMNVBSTAUHUAUKUSUBBAUCUDUEUFULUNUTUMVAUGU
      IUJ $.
      $( [2-Oct-1999] $)

    $( Imaginary part of a complex conjugate. $)
    imcji $p |- ( Im ` ( * ` A ) ) = -u ( Im ` A ) $=
      ( ccj cfv cim cre ci cneg cmul caddc cmin recli recni ax-icn imcli mulcli
      co negsubi mulneg2i oveq2i wcel wceq remim ax-mp 3eqtr4ri fveq2i renegcli
      cc crimi eqtri ) ACDZEDAFDZGAEDZHZIQZJQZEDUNUKUPEULGUMIQZHZJQULUQKQZUPUKU
      LUQULABLZMGUMNUMABOZMZPRUOURULJGUMNVBSTAUHUAUKUSUBBAUCUDUEUFULUNUTUMVAUGU
      IUJ $.
      $( [2-Oct-1999] $)

    ${
      readdi.2 $e |- B e. CC $.
      $( Real part distributes over addition. $)
      readdi $p |- ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) $=
        ( caddc co cre cfv ci cim recli recni ax-icn imcli mulcli add4i replimi
        cmul oveq12i readdcli adddii oveq2i 3eqtr4i fveq2i crrei eqtri ) ABEFZG
        HAGHZBGHZEFZIAJHZBJHZEFZRFZEFZGHUJUGUOGUHIUKRFZEFZUIIULRFZEFZEFUJUPUREF
        ZEFUGUOUHUPUIURUHACKZLIUKMUKACNZLZOUIBDKZLIULMULBDNZLZOPAUQBUSEACQBDQSU
        NUTUJEIUKULMVCVFUAUBUCUDUJUMUHUIVAVDTUKULVBVETUEUF $.
        $( [28-Jul-1999] $)

      $( Imaginary part distributes over addition. $)
      imaddi $p |- ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) $=
        ( caddc co cim cfv cre ci recli recni ax-icn imcli mulcli add4i replimi
        cmul oveq12i readdcli adddii oveq2i 3eqtr4i fveq2i crimi eqtri ) ABEFZG
        HAIHZBIHZEFZJAGHZBGHZEFZRFZEFZGHUMUGUOGUHJUKRFZEFZUIJULRFZEFZEFUJUPUREF
        ZEFUGUOUHUPUIURUHACKZLJUKMUKACNZLZOUIBDKZLJULMULBDNZLZOPAUQBUSEACQBDQSU
        NUTUJEJUKULMVCVFUAUBUCUDUJUMUHUIVAVDTUKULVBVETUEUF $.
        $( [28-Jul-1999] $)

      $( Real part of a product. $)
      remuli $p |- ( Re ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Re ` B ) ) - ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( cmul co cre cfv cim ci caddc replimi oveq12i recli recni imcli crmuli
        cmin eqtri remulcli fveq2i resubcli readdcli crrei ) ABEFZGHAGHZBGHZEFZ
        AIHZBIHZEFZRFZJUFUJEFZUIUGEFZKFZEFKFZGHULUEUPGUEUFJUIEFKFZUGJUJEFKFZEFU
        PAUQBUREACLBDLMUFUIUGUJUFACNZOUIACPZOUGBDNZOUJBDPZOQSUAULUOUHUKUFUGUSVA
        TUIUJUTVBTUBUMUNUFUJUSVBTUIUGUTVATUCUDS $.
        $( [28-Jul-1999] $)

      $( Imaginary part of a product. $)
      immuli $p |- ( Im ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Im ` B ) ) + ( ( Im ` A ) x. ( Re ` B ) ) ) $=
        ( cmul co cim cfv cre ci caddc replimi oveq12i recli recni imcli crmuli
        cmin eqtri remulcli fveq2i resubcli readdcli crimi ) ABEFZGHAIHZBIHZEFZ
        AGHZBGHZEFZRFZJUFUJEFZUIUGEFZKFZEFKFZGHUOUEUPGUEUFJUIEFKFZUGJUJEFKFZEFU
        PAUQBUREACLBDLMUFUIUGUJUFACNZOUIACPZOUGBDNZOUJBDPZOQSUAULUOUHUKUFUGUSVA
        TUIUJUTVBTUBUMUNUFUJUSVBTUIUGUTVATUCUDS $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
         [Gleason] p. 133. $)
      cjaddi $p |- ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) $=
        ( caddc co cre cfv ci cim cmul cmin ccj ax-icn recni cc wcel wceq remim
        ax-mp readdi imaddi oveq2i imcli adddii oveq12i mulcli addsub4i 3eqtr4i
        eqtri recli addcli ) ABEFZGHZIUMJHZKFZLFZAGHZIAJHZKFZLFZBGHZIBJHZKFZLFZ
        EFZUMMHZAMHZBMHZEFUQURVBEFZUTVDEFZLFVFUNVJUPVKLABCDUAUPIUSVCEFZKFVKUOVL
        IKABCDUBUCIUSVCNUSACUDOZVCBDUDOZUEUJUFURVBUTVDURACUKOVBBDUKOIUSNVMUGIVC
        NVNUGUHUJUMPQVGUQRABCDULUMSTVHVAVIVEEAPQVHVARCASTBPQVIVERDBSTUFUI $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over multiplication.  Proposition
         10-3.4(c) of [Gleason] p. 133. $)
      cjmuli $p |- ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) $=
        ( cmul co cre cfv ci cim cmin cneg caddc ccj recni mulcli ax-icn oveq2i
        mulneg2i oveq12i recli imcli subcli addcli negsubi eqcomi negdii eqtr4i
        mul2negi mulneg1i eqtr3i remuli immuli negcli crmuli 3eqtr4i wcel remim
        cc wceq ax-mp 3eqtr4ri ) ABEFZGHZIVCJHZEFZKFZAGHZIAJHZLZEFZMFZBGHZIBJHZ
        LZEFZMFZEFZVCNHZANHZBNHZEFVHVMEFZVIVNEFZKFZIVHVNEFZVIVMEFZMFZEFZKFZWBVJ
        VOEFZKFZIVHVOEFZVJVMEFZMFZEFZMFZVGVRWDWHLZMFWIWPWDWHWBWCVHVMVHACUAOZVMB
        DUAOZPVIVNVIACUBOZVNBDUBOZPUCIWGQWEWFVHVNWRXAPZVIVMWTWSPZUDZPUEWDWKWQWO
        MWCWJWBKWJWCVIVNWTXAUIUFRIWGLZEFWQWOIWGQXDSXEWNIEXEWELZWFLZMFWNWEWFXBXC
        UGWLXFWMXGMVHVNWRXASVIVMWTWSUJTUHRUKTUKVDWDVFWHKABCDULVEWGIEABCDUMRTVHV
        JVMVOWRVIWTUNWSVNXAUNUOUPVCUSUQVSVGUTABCDPVCURVAVTVLWAVQEVHIVIEFZLZMFVH
        XHKFZVLVTVHXHWRIVIQWTPUEVKXIVHMIVIQWTSRAUSUQVTXJUTCAURVAVBVMIVNEFZLZMFV
        MXKKFZVQWAVMXKWSIVNQXAPUEVPXLVMMIVNQXASRBUSUQWAXMUTDBURVAVBTUP $.
        $( [28-Jul-1999] $)

      $( Standard inner product on complex numbers. $)
      ipcni $p |- ( Re ` ( A x. ( * ` B ) ) ) =
             ( ( ( Re ` A ) x. ( Re ` B ) ) + ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( ccj cfv cmul cre cim cmin cneg caddc cjcli recli remulcli recni imcli
        co oveq2i wceq remuli negsubi recji imcji negcon2i mpbi mulneg2i eqtr2i
        oveq12i 3eqtr2i ) ABEFZGRHFAHFZUKHFZGRZAIFZUKIFZGRZJRUNUQKZLRULBHFZGRZU
        OBIFZGRZLRAUKCBDMZUAUNUQUNULUMACNUKVCNOPUQUOUPACQZUKVCQZOPUBUNUTURVBLUM
        USULGBDUCSVBUOUPKZGRURVAVFUOGUPVAKTVAVFTBDUDUPVAUPVEPZVABDQPUEUFSUOUPUO
        VDPVGUGUHUIUJ $.
        $( [2-Oct-1999] $)
    $}

    $( A complex number times its conjugate is real. $)
    cjmulrcli $p |- ( A x. ( * ` A ) ) e. RR $=
      ( ccj cfv cmul co cr wcel wceq cjcji oveq2i cjmuli mulcomi 3eqtr4i mulcli
      cjcli cjrebi mpbir ) AACDZEFZGHTCDZTISSCDZEFSAEFUATUBASEABJKASBABPZLASBUC
      MNTASBUCOQR $.
      $( [11-May-1999] $)

    $( A complex number times its conjugate. $)
    cjmulvali $p |- ( A x. ( * ` A ) ) =
                   ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) $=
      ( cre cfv c2 cexp co cim caddc ccj recli recni sqvali imcli oveq12i ipcni
      cmul cr wcel wceq cjmulrcli rerebi mpbi 3eqtr2ri ) ACDZEFGZAHDZEFGZIGUEUE
      QGZUGUGQGZIGAAJDQGZCDZUKUFUIUHUJIUEUEABKLMUGUGABNLMOAABBPUKRSULUKTABUAZUK
      UKUMLUBUCUD $.
      $( [2-Oct-1999] $)

    $( A complex number times its conjugate is nonnegative. $)
    cjmulge0i $p |- 0 <_ ( A x. ( * ` A ) ) $=
      ( cc0 cre cfv c2 cexp co cim caddc ccj cle wbr recli sqge0i imcli resqcli
      cmul addge0i mp2an cjmulvali breqtrri ) CADEZFGHZAIEZFGHZJHZAAKERHLCUDLMC
      UFLMCUGLMUCABNZOUEABPZOUDUFUCUHQUEUIQSTABUAUB $.
      $( [28-May-1999] $)

    $( Real part of negative. $)
    renegi $p |- ( Re ` -u A ) = -u ( Re ` A ) $=
      ( cneg cre cfv ci cim cmul caddc recli recni ax-icn mulcli negdii replimi
      co imcli negeqi mulneg2i renegcli oveq2i 3eqtr4i fveq2i crrei eqtri ) ACZ
      DEADEZCZFAGEZCZHPZIPZDEUHUFULDUGFUIHPZIPZCUHUMCZIPUFULUGUMUGABJZKFUILUIAB
      QZKZMNAUNABORUKUOUHIFUILURSUAUBUCUHUJUGUPTUIUQTUDUE $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( The negative of a real is real. $)
    negrebi $p |- ( -u A e. RR <-> A e. RR ) $=
      ( cneg wcel cre cfv negcli rerebi renegi eqeq1i recli recni neg11i 3bitri
      cr wceq eleq1 mpbii sylbi renegcl impbii ) ACZODZAODZUCAEFZAPZUDUCUBEFZUB
      PUECZUBPUFUBABGHUGUHUBABIJUEAUEABKZLBMNUFUEODUDUIUEAOQRSATUA $.
      $( [11-Aug-1999] $)

    $( Imaginary part of negative. $)
    imnegi $p |- ( Im ` -u A ) = -u ( Im ` A ) $=
      ( cneg cim cfv cre ci cmul caddc recli recni ax-icn mulcli negdii replimi
      co imcli negeqi mulneg2i renegcli oveq2i 3eqtr4i fveq2i crimi eqtri ) ACZ
      DEAFEZCZGADEZCZHPZIPZDEUJUFULDUGGUIHPZIPZCUHUMCZIPUFULUGUMUGABJZKGUILUIAB
      QZKZMNAUNABORUKUOUHIGUILURSUAUBUCUHUJUGUPTUIUQTUDUE $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( Complex conjugate of negative. $)
    cjnegi $p |- ( * ` -u A ) = -u ( * ` A ) $=
      ( cneg cre cfv ci cim cmul co ccj caddc recni negcli ax-icn negsubi eqtri
      cmin negeqi cc wcel mulcli negdii renegi imnegi mulneg2i oveq12i 3eqtr4ri
      recli imcli oveq2i wceq remim ax-mp 3eqtr4i ) ACZDEZFUOGEZHIZQIZADEZFAGEZ
      HIZQIZCZUOJEZAJEZCUSUTVBCZKIZCZVDUTCZVGCKIVJVGQIVIUSVJVGUTUTABUHLZMVBFVAN
      VAABUILZUAZMZOUTVGVKVNUBUPVJURVGQABUCURFVACZHIVGUQVOFHABUDUJFVANVLUEPUFUG
      VHVCUTVBVKVMORPUOSTVEUSUKABMUOULUMVFVCASTVFVCUKBAULUMRUN $.
      $( [2-Aug-1999] $)

    $( A number plus its conjugate is twice its real part.  Compare Proposition
       10-3.4(h) of [Gleason] p. 133. $)
    addcji $p |- ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) $=
      ( cre cfv ci cim cmul co caddc ccj c2 cc0 recli recni ax-icn imcli mulcli
      cneg negcli add4i negidi oveq2i addcli addid1i replimi cmin cc wcel remim
      3eqtri wceq ax-mp negsubi eqtr4i oveq12i 2timesi 3eqtr4i ) ACDZEAFDZGHZIH
      ZURUTRZIHZIHZURURIHZAAJDZIHKURGHVDVEUTVBIHZIHVELIHVEURUTURVBURABMNZEUSOUS
      ABPNQZVHUTVISTVGLVEIUTVIUAUBVEURURVHVHUCUDUJAVAVFVCIABUEVFURUTUFHZVCAUGUH
      VFVJUKBAUIULURUTVHVIUMUNUOURVHUPUQ $.
      $( [2-Oct-1999] $)
  $}

  $( A real number equals its real part.  One direction of Proposition
     10-3.4(f) of [Gleason] p. 133.  (Contributed by Paul Chapman,
     7-Sep-2007.) $)
  rere $p |- ( A e. RR -> ( Re ` A ) = A ) $=
    ( cr wcel cre cfv wceq cc wb recn rereb syl ibi ) ABCZADEAFZMAGCMNHAIAJKL
    $.
    $( [7-Sep-2007] $)

  $( A number is real iff it equals its complex conjugate.  Proposition
     10-3.4(f) of [Gleason] p. 133. $)
  cjreb $p |- ( A e. CC -> ( A e. RR <-> ( * ` A ) = A ) ) $=
    ( cc wcel cr ccj cfv wceq wb cc0 cif eleq1 fveq2 eqeq12d bibi12d 0cn elimel
    id cjrebi dedth ) ABCZADCZAEFZAGZHTAIJZDCZUDEFZUDGZHAIAUDGZUAUEUCUGAUDDKUHU
    BUFAUDAUDELUHQMNUDAIBOPRS $.
    $( [2-Jul-2005] $)

  $( A complex number times its conjugate is real. $)
  cjmulrcl $p |- ( A e. CC -> ( A x. ( * ` A ) ) e. RR ) $=
    ( cc wcel ccj cfv cmul co cr cc0 cif wceq id fveq2 oveq12d eleq1d cjmulrcli
    0cn elimel dedth ) ABCZAADEZFGZHCTAIJZUCDEZFGZHCAIAUCKZUBUEHUFAUCUAUDFUFLAU
    CDMNOUCAIBQRPS $.
    $( [26-Mar-2005] $)

  $( A complex number times its conjugate. $)
  cjmulval $p |- ( A e. CC -> ( A x. ( * ` A ) ) =
                 ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) ) $=
    ( cc wcel ccj cfv cmul co cre c2 cexp cim caddc cif id fveq2 oveq12d oveq1d
    wceq eqeq12d 2cn elimel cjmulvali dedth ) ABCZAADEZFGZAHEZIJGZAKEZIJGZLGZRU
    DAIMZULDEZFGZULHEZIJGZULKEZIJGZLGZRAIAULRZUFUNUKUSUTAULUEUMFUTNAULDOPUTUHUP
    UJURLUTUGUOIJAULHOQUTUIUQIJAULKOQPSULAIBTUAUBUC $.
    $( [1-Feb-2007] $)

  $( A complex number times its conjugate is nonnegative. $)
  cjmulge0 $p |- ( A e. CC -> 0 <_ ( A x. ( * ` A ) ) ) $=
    ( cc wcel cc0 ccj cfv cmul co cle wbr cif wceq id oveq12d breq2d 0cn elimel
    fveq2 cjmulge0i dedth ) ABCZDAAEFZGHZIJDUAADKZUDEFZGHZIJADAUDLZUCUFDIUGAUDU
    BUEGUGMAUDERNOUDADBPQST $.
    $( [26-Mar-2005] $)

  $( Real part of negative. $)
  reneg $p |- ( A e. CC -> ( Re ` -u A ) = -u ( Re ` A ) ) $=
    ( cc wcel cneg cre cfv cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn elimel
    wceq renegi dedth ) ABCZADZEFZAEFZDZPSAGHZDZEFZUDEFZDZPAGAUDPZUAUFUCUHUITUE
    EAUDIJUIUBUGAUDEKLMUDAGBNOQR $.
    $( [17-Mar-2005] $)

  $( Real part distributes over addition. $)
  readd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) ) $=
    ( cc wcel caddc co cre cfv wceq cc0 oveq1 fveq2d fveq2 oveq1d eqeq12d oveq2
    cif oveq2d 0cn elimel readdi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZEFZIUCAJQZBE
    FZGHZUJGHZUHEFZIUJUDBJQZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEUKGAUJBEKLUTU
    GUMUHEAUJGMNOBUOIZULUQUNUSVAUKUPGBUOUJEPLVAUHURUMEBUOGMROUJUOAJCSTBJCSTUAUB
    $.
    $( [17-Mar-2005] $)

  $( Real part distributes over subtraction. $)
  resub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A - B ) ) = ( ( Re ` A ) - ( Re ` B ) ) ) $=
    ( cc wcel wa cneg caddc cre cfv cmin negcl readd sylan2 reneg adantl negsub
    co wceq recl recnd oveq2d eqtrd fveq2d syl2an 3eqtr3d ) ACDZBCDZEZABFZGQZHI
    ZAHIZBHIZFZGQZABJQZHIULUMJQZUHUKULUIHIZGQZUOUGUFUICDUKUSRBKAUILMUHURUNULGUG
    URUNRUFBNOUAUBUHUJUPHABPUCUFULCDUMCDUOUQRUGUFULASTUGUMBSTULUMPUDUE $.
    $( [2-Aug-2006] $) $( [17-Mar-2005] $)

  $( The imaginary part of a negative number. $)
  imneg $p |- ( A e. CC -> ( Im ` -u A ) = -u ( Im ` A ) ) $=
    ( cc wcel cneg cim cfv cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn elimel
    wceq imnegi dedth ) ABCZADZEFZAEFZDZPSAGHZDZEFZUDEFZDZPAGAUDPZUAUFUCUHUITUE
    EAUDIJUIUBUGAUDEKLMUDAGBNOQR $.
    $( [18-Mar-2005] $)

  $( Imaginary part distributes over addition. $)
  imadd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) ) $=
    ( cc wcel caddc co cim cfv wceq cc0 oveq1 fveq2d fveq2 oveq1d eqeq12d oveq2
    cif oveq2d 0cn elimel imaddi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZEFZIUCAJQZBE
    FZGHZUJGHZUHEFZIUJUDBJQZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEUKGAUJBEKLUTU
    GUMUHEAUJGMNOBUOIZULUQUNUSVAUKUPGBUOUJEPLVAUHURUMEBUOGMROUJUOAJCSTBJCSTUAUB
    $.
    $( [18-Mar-2005] $)

  $( Imaginary part distributes over subtraction. $)
  imsub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A - B ) ) = ( ( Im ` A ) - ( Im ` B ) ) ) $=
    ( cc wcel wa cneg caddc cim cfv cmin negcl imadd sylan2 imneg adantl negsub
    co wceq imcl recnd oveq2d eqtrd fveq2d syl2an 3eqtr3d ) ACDZBCDZEZABFZGQZHI
    ZAHIZBHIZFZGQZABJQZHIULUMJQZUHUKULUIHIZGQZUOUGUFUICDUKUSRBKAUILMUHURUNULGUG
    URUNRUFBNOUAUBUHUJUPHABPUCUFULCDUMCDUOUQRUGUFULASTUGUMBSTULUMPUDUE $.
    $( [1-Aug-2006] $) $( [18-Mar-2005] $)

  $( A real number equals its complex conjugate.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  cjre $p |- ( A e. RR -> ( * ` A ) = A ) $=
    ( cc wcel cr ccj cfv wceq recn cjreb biimpd mpcom ) ABCZADCZAEFAGZAHLMNAIJK
    $.
    $( [8-Oct-1999] $)

  $( The conjugate of the conjugate is the original complex number.
     Proposition 10-3.4(e) of [Gleason] p. 133. $)
  cjcj $p |- ( A e. CC -> ( * ` ( * ` A ) ) = A ) $=
    ( cc wcel ccj cfv wceq cc0 cif fveq2 fveq2d eqeq12d 0cn elimel cjcji dedth
    id ) ABCZADEZDEZAFQAGHZDEZDEZTFAGATFZSUBATUCRUADATDIJUCPKTAGBLMNO $.
    $( [29-Jul-1999] $)

  $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
     [Gleason] p. 133. $)
  cjadd $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) ) $=
    ( cc wcel caddc co ccj cfv wceq cc0 oveq1 fveq2d fveq2 oveq1d eqeq12d oveq2
    cif oveq2d 0cn elimel cjaddi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZEFZIUCAJQZBE
    FZGHZUJGHZUHEFZIUJUDBJQZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEUKGAUJBEKLUTU
    GUMUHEAUJGMNOBUOIZULUQUNUSVAUKUPGBUOUJEPLVAUHURUMEBUOGMROUJUOAJCSTBJCSTUAUB
    $.
    $( [31-Jul-1999] $)

  $( Complex conjugate distributes over multiplication.  Proposition 10-3.4(c)
     of [Gleason] p. 133. $)
  cjmul $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) ) $=
    ( cc wcel cmul ccj cfv wceq cc0 cif oveq1 fveq2d fveq2 oveq1d eqeq12d oveq2
    co oveq2d 0cn elimel cjmuli dedth2h ) ACDZBCDZABEQZFGZAFGZBFGZEQZHUCAIJZBEQ
    ZFGZUJFGZUHEQZHUJUDBIJZEQZFGZUMUOFGZEQZHABIIAUJHZUFULUIUNUTUEUKFAUJBEKLUTUG
    UMUHEAUJFMNOBUOHZULUQUNUSVAUKUPFBUOUJEPLVAUHURUMEBUOFMROUJUOAICSTBICSTUAUB
    $.
    $( [29-Jul-1999] $)

  $( Complex conjugate of negative. $)
  cjneg $p |- ( A e. CC -> ( * ` -u A ) = -u ( * ` A ) ) $=
    ( cc wcel cneg ccj cfv cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn elimel
    wceq cjnegi dedth ) ABCZADZEFZAEFZDZPSAGHZDZEFZUDEFZDZPAGAUDPZUAUFUCUHUITUE
    EAUDIJUIUBUGAUDEKLMUDAGBNOQR $.
    $( [27-Feb-2005] $)

  $( A number plus its conjugate is twice its real part.  Compare Proposition
     10-3.4(h) of [Gleason] p. 133. $)
  addcj $p |- ( A e. CC -> ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) ) $=
    ( cc wcel ccj cfv caddc co c2 cre cmul wceq cc0 cif id fveq2 oveq12d oveq2d
    eqeq12d 0cn elimel addcji dedth ) ABCZAADEZFGZHAIEZJGZKUCALMZUHDEZFGZHUHIEZ
    JGZKALAUHKZUEUJUGULUMAUHUDUIFUMNAUHDOPUMUFUKHJAUHIOQRUHALBSTUAUB $.
    $( [21-Jan-2007] $)

  $( Complex conjugate distributes over subtraction. $)
  cjsub $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A - B ) ) = ( ( * ` A ) - ( * ` B ) ) ) $=
    ( cc wcel wa cneg caddc ccj cfv cmin negcl cjadd sylan2 negsub fveq2d cjneg
    co wceq adantl cjcl oveq2d syl2an eqtrd 3eqtr3d ) ACDZBCDZEZABFZGQZHIZAHIZU
    HHIZGQZABJQZHIUKBHIZJQZUFUEUHCDUJUMRBKAUHLMUGUIUNHABNOUGUMUKUOFZGQZUPUGULUQ
    UKGUFULUQRUEBPSUAUEUKCDUOCDURUPRUFATBTUKUONUBUCUD $.
    $( [1-Aug-2006] $) $( [28-Apr-2005] $)

  ${
    $d j k A $.  $d j k N $.
    $( Complex conjugate of natural number exponentiation. $)
    cjexp $p |- ( ( A e. CC /\ N e. NN0 ) ->
               ( * ` ( A ^ N ) ) = ( ( * ` A ) ^ N ) ) $=
      ( vj vk cn0 wcel cc cexp co ccj cfv wi cc0 c1 oveq2 fveq2d eqeq12d imbi2d
      wceq cmul cv caddc exp0 cjcl cr 1re cjre ax-mp syl6eqr eqtr4d expp1 expcl
      syl wa simpl cjmul syl2anc eqtrd adantr oveq1 sylan sylan9eqr exp31 com12
      eqcomd a2d nn0ind impcom ) BEFAGFZABHIZJKZAJKZBHIZSZVIACUAZHIZJKZVLVOHIZS
      ZLVIAMHIZJKZVLMHIZSZLVIADUAZHIZJKZVLWDHIZSZLVIAWDNUBIZHIZJKZVLWIHIZSZLVIV
      NLCDBVOMSZVSWCVIWNVQWAVRWBWNVPVTJVOMAHOPVOMVLHOQRVOWDSZVSWHVIWOVQWFVRWGWO
      VPWEJVOWDAHOPVOWDVLHOQRVOWISZVSWMVIWPVQWKVRWLWPVPWJJVOWIAHOPVOWIVLHOQRVOB
      SZVSVNVIWQVQVKVRVMWQVPVJJVOBAHOPVOBVLHOQRVIWANJKZWBVIVTNJAUCPVIVLGFZWBWRS
      AUDZWSWBNWRVLUCNUEFWRNSUFNUGUHUIUMUJWDEFZVIWHWMVIXAWHWMLVIXAWHWMVIXAUNZWH
      UNWKWFVLTIZWLXBWKXCSWHXBWKWEATIZJKZXCXBWJXDJAWDUKPXBWEGFVIXEXCSAWDULVIXAU
      OWEAUPUQURUSWHXBXCWGVLTIZWLWFWGVLTUTXBWLXFVIWSXAWLXFSWTVLWDUKVAVEVBURVCVD
      VFVGVH $.
      $( [7-Jun-2006] $)
  $}

  $( The real part of a number in terms of complex conjugate. $)
  recj $p |- ( A e. CC -> ( Re ` A ) = ( ( A + ( * ` A ) ) / 2 ) ) $=
    ( cc wcel cre cfv caddc co c2 cdiv ccj wceq recl recnd wa syl2anc ci ax-icn
    cc0 oveq2d 3syl wne 2cn 2ne0 pm3.2i divdir mp3an3 cim cmul cmin cneg replim
    remim oveq12d imcl mulcl sylancr negsub mpan negid negcl syl22anc jca addcl
    add4 addid1 3eqtr3d 3eqtr2rd oveq1d 2halves syl 3eqtr3rd ) ABCZADEZVMFGZHIG
    ZVMHIGZVPFGZAAJEZFGZHIGVMVLVMBCZVTVOVQKZVLVMALMZWBVTVTHBCZHRUAZNWAWCWDUBUCU
    DVMVMHUEUFOVLVNVSHIVLVSVMPAUGEZUHGZFGZVMWFUIGZFGWGVMWFUJZFGZFGZVNVLAWGVRWHF
    AUKAULUMVLWJWHWGFVLVTWFBCZWJWHKWBVLPBCZWEBCZWLQVLWEAUNMZPWEUOZUPZVMWFUQOSVL
    VNWFWIFGZFGZVNRFGZWKVNVLWRRVNFVLWNWLWRRKWOWMWNWLQWPURZWFUSTSVLVTVTWLWIBCZWS
    WKKWBWBWQVLWNWLXBWOXAWFUTTVMVMWFWIVDVAVLVTVTNVNBCWTVNKVLVTVTWBWBVBVMVMVCVNV
    ETVFVGVHVLVTVQVMKWBVMVIVJVK $.
    $( [2-Aug-2006] $) $( [30-Apr-2005] $)

  $( The imaginary part of a number in terms of complex conjugate. $)
  imcj $p |- ( A e. CC -> ( Im ` A ) = ( ( A - ( * ` A ) ) / ( 2 x. _i ) ) ) $=
    ( cc wcel cim cfv c2 ci cmul co cdiv ccj cmin wceq recnd 2cn ax-icn syl2anc
    syl caddc oveq1d imcl cc0 wne mulcli 2ne0 ine0 mulne0i divcan4 mp3an23 recl
    mulcl sylancr addcl subsub syl3anc replim remim oveq12d 2times mulcom mpan2
    cre mulass mp3an12 eqtrd pncan2 3eqtr4d 3eqtr4rd eqtr3d ) ABCZADEZFGHIZHIZV
    LJIZVKAAKEZLIZVLJIVJVKBCZVNVKMZVJVKAUANZVQVLBCZVLUBUCVRFGOPUDZFGOPUEUFUGVKV
    LUHUIRVJVMVPVLJVJAVBEZGVKHIZSIZWBWCLIZLIZWDWBLIZWCSIZVPVMVJWDBCZWBBCZWCBCZW
    FWHMVJWJWKWIVJWBAUJNZVJGBCZVQWKPVSGVKUKULZWBWCUMQWLWNWDWBWCUNUOVJAWDVOWELAU
    PAUQURVJFWCHIZWCWCSIZVMWHVJWKWOWPMWNWCUSRVJVQVMWOMVSVQVMVLVKHIZWOVQVTVMWQMW
    AVKVLUTVAFBCWMVQWQWOMOPFGVKVCVDVERVJWGWCWCSVJWJWKWGWCMWLWNWBWCVFQTVGVHTVI
    $.
    $( [30-Apr-2005] $)

  $( The real part of zero. $)
  re0 $p |- ( Re ` 0 ) = 0 $=
    ( cc0 cr wcel cre cfv wceq 0re 0cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The imaginary part of zero. $)
  im0 $p |- ( Im ` 0 ) = 0 $=
    ( cc0 cr wcel cim cfv wceq 0re 0cn reim0bi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The real part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  re1 $p |- ( Re ` 1 ) = 1 $=
    ( c1 cr wcel cre cfv wceq 1re ax-1cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [9-Jun-2006] $)

  $( The imaginary part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  im1 $p |- ( Im ` 1 ) = 0 $=
    ( c1 cr wcel cim cfv cc0 wceq 1re ax-1cn reim0bi mpbi ) ABCADEFGHAIJK $.
    $( [9-Jun-2006] $)

  $( The real part of ` _i ` .  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  rei $p |- ( Re ` _i ) = 0 $=
    ( cc0 ci c1 cmul co caddc cre cfv ax-icn ax-1cn mulcli addid2i fveq2i crrei
    0re 1re mulid1i 3eqtr3ri ) ABCDEZFEZGHSGHABGHTSGSBCIJKLMACOPNSBGBIQMR $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The imaginary part of ` _i ` .  (Contributed by Scott Fenton,
     9-Jun-2006.) $)
  imi $p |- ( Im ` _i ) = 1 $=
    ( ci c1 cmul cim cfv cc0 ax-icn ax-1cn mulcli addid2i eqcomi fveq2i mulid1i
    co caddc 0re 1re crimi 3eqtr3i ) ABCNZDEFTONZDEADEBTUADUATTABGHIJKLTADAGMLF
    BPQRS $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The conjugate of zero. $)
  cj0 $p |- ( * ` 0 ) = 0 $=
    ( cc0 cr wcel ccj cfv wceq 0re 0cn cjrebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The complex conjugate of the imaginary unit. $)
  cji $p |- ( * ` _i ) = -u _i $=
    ( ci cre cfv cim cmul co cmin cc0 ccj cneg rei c1 imi oveq2i ax-icn mulid1i
    eqtri oveq12i cc wcel wceq remim ax-mp df-neg 3eqtr4i ) ABCZAADCZEFZGFZHAGF
    AICZAJUFHUHAGKUHALEFAUGLAEMNAOPQRASTUJUIUAOAUBUCAUDUE $.
    $( [26-Mar-2005] $)

  $( The conjugate of a representation of a complex number in terms of real and
     imaginary parts. $)
  cjreim $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A + ( _i x. B ) ) ) =
                ( A - ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul co caddc ccj cfv cneg cc wceq recn ax-icn sylancr cjre
    syl2an 3eqtrd cmin mulcl cjadd cjmul cji oveq12d mulneg1 oveqan12d negsub
    a1i ) ACDZBCDZEAFBGHZIHJKZAJKZUMJKZIHZAUMLZIHZAUMUAHZUKAMDZUMMDZUNUQNULAOZU
    LFMDZBMDZVBPBOZFBUBQZAUMUCSUKULUOAUPURIARULUPFJKZBJKZGHZFLZBGHZURULVDVEUPVJ
    NPVFFBUDQULVHVKVIBGVHVKNULUEUJBRUFULVDVEVLURNPVFFBUGQTUHUKVAVBUSUTNULVCVGAU
    MUIST $.
    $( [1-Aug-2006] $) $( [1-Jul-2005] $)

  $( The conjugate of the representation of a complex number in terms of real
     and imaginary parts. $)
  cjreim2 $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A - ( _i x. B ) ) ) =
                ( A + ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul cmin ccj cfv cneg wceq recn ax-icn sylancr syl2an cjre
    co cc 3eqtrd caddc mulcl cjsub cjmul cji oveq12d mulneg1 oveqan12d subneg
    a1i ) ACDZBCDZEAFBGRZHRIJZAIJZUMIJZHRZAUMKZHRZAUMUARZUKASDZUMSDZUNUQLULAMZU
    LFSDZBSDZVBNBMZFBUBOZAUMUCPUKULUOAUPURHAQULUPFIJZBIJZGRZFKZBGRZURULVDVEUPVJ
    LNVFFBUDOULVHVKVIBGVHVKLULUEUJBQUFULVDVEVLURLNVFFBUGOTUHUKVAVBUSUTLULVCVGAU
    MUIPT $.
    $( [1-Jul-2005] $)

  $( Complex conjugate is a one-to-one function.  (Proof shortened by Eric
     Schmidt, 2-Jul-2009.) $)
  cj11 $p |- ( ( A e. CC /\ B e. CC ) ->
              ( ( * ` A ) = ( * ` B ) <-> A = B ) ) $=
    ( cc wcel wa ccj cfv wceq fveq2 cjcj eqeqan12d syl5ib impbid1 ) ACDZBCDZEZA
    FGZBFGZHZABHZSQFGZRFGZHPTQRFINOUAAUBBAJBJKLABFIM $.
    $( [2-Jul-2009] $) $( [29-Apr-2005] $)

  $( A number is nonzero iff its complex conjugate is nonzero. $)
  cjne0 $p |- ( A e. CC -> ( A =/= 0 <-> ( * ` A ) =/= 0 ) ) $=
    ( cc wcel cc0 ccj cfv wceq cj0 eqeq2i wb 0cn cj11 mpan2 syl5rbbr necon3bid
    ) ABCZADAEFZDQDGQDEFZGZPADGZRDQHIPDBCSTJKADLMNO $.
    $( [29-Apr-2005] $)

  ${
    cjdiv.1 $e |- A e. CC $.
    cjdiv.2 $e |- B e. CC $.
    $( Complex conjugate distributes over division. $)
    cjdivi $p |- ( B =/= 0 ->
                    ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
      ( cc0 wne ccj cfv cdiv co wceq cmul cc divclzi cjmul sylancr divcan2zi wb
      wcel cjcli fveq2d eqtr3d cjcl syl 0cn cj11 mp2an cj0 eqeq2i bitr3i biimpi
      necon3bii wa divmul mp3an1 mpanr1 syl2anc mpbird eqcomd ) BEFZAGHZBGHZIJZ
      ABIJZGHZUTVCVEKZVBVELJZVAKZUTBVDLJZGHZVGVAUTBMSZVDMSZVJVGKDABCDNZBVDOPUTV
      IAGABCDQUAUBUTVEMSZVBEFZVFVHRZUTVLVNVMVDUCUDUTVOBEVBEBEKZVBEGHZKZVBEKVKEM
      SVSVQRDUEBEUFUGVREVBUHUIUJULUKVNVBMSZVOVPBDTVAMSVNVTVOUMVPACTVAVEVBUNUOUP
      UQURUS $.
      $( [6-Sep-2013] $) $( [29-Apr-2005] $)
  $}

  $( Complex conjugate distributes over division. $)
  cjdiv $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                   ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
    ( cc wcel cc0 wne cdiv co ccj cfv wceq wi oveq1 fveq2d fveq2 oveq1d eqeq12d
    cif 0cn elimel imbi2d neeq1 oveq2 oveq2d imbi12d cjdivi dedth2h 3impia ) AC
    DZBCDZBEFZABGHZIJZAIJZBIJZGHZKZUIUJUKUQLUKUIAERZBGHZIJZURIJZUOGHZKZLUJBERZE
    FZURVDGHZIJZVAVDIJZGHZKZLABEEAURKZUQVCUKVKUMUTUPVBVKULUSIAURBGMNVKUNVAUOGAU
    RIOPQUABVDKZUKVEVCVJBVDEUBVLUTVGVBVIVLUSVFIBVDURGUCNVLUOVHVAGBVDIOUDQUEURVD
    AECSTBECSTUFUGUH $.
    $( [3-Feb-2006] $) $( [29-Apr-2005] $)

