$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Introduce Grothendieck's Axiom
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u f $.
    $( Grothendieck's Axiom.  For every set ` x ` there is an inaccessible
       cardinal ` y ` such that ` y ` is not in ` x ` . The addition of this
       axiom to ZFC set theory provides a framework for category theory,
       thus for all practical purposes giving us a complete foundation for
       "all of mathematics."  This version of the axiom is used by the Mizar
       project ( ~ http://www.mizar.org/JFM/Axiomatics/tarski.html ).  Unlike
       the ZFC axioms, this axiom is very long when expressed in terms of
       primitive symbols - see ~ grothprim .  An open problem is finding a
       shorter equivalent. $)
    ax-groth $a |- E. y ( x e. y /\ A. z e. y ( A. w ( w (_ z -> w e. y ) /\
                       E. w e. y A. v ( v (_ z -> v e. w ) ) /\
                     A. z ( z (_ y -> ( z ~~ y \/ z e. y ) ) ) $.

    $( Alternate version of Grothendieck's Axiom. $)
    axgroth2 $p |- E. y ( x e. y /\ A. z e. y ( A. w ( w (_ z -> w e. y ) /\
                       E. w e. y A. v ( v (_ z -> v e. w ) ) /\
                     A. z ( z (_ y -> ( y ~<_ z \/ z e. y ) ) ) $=
      ( cv wcel wss wi wal wrex wa wral cdom wbr wo w3a wex cen ax-groth cvv
      visset ssdomg ax-mp biantrurd wb sbthbg syl6bb orbi1d pm5.74i albii
      3anbi3i exbii mpbir ) AFBFZGZDFZCFZHUQUOGIDJEFZURHUSUQGIEJDUOKLCUOMZURUOH
      ZUOURNOZURUOGZPZIZCJZQZBRUPUTVAURUOSOZVCPZIZCJZQZBRABCDETVGVLBVFVKUPUTVEV
      JCVAVDVIVAVBVHVCVAVBURUONOZVBLZVHVAVMVBURUAGVAVMICUBURUOUAUCUDUEUOUAGVNVH
      UFBUBURUOUAUGUDUHUIUJUKULUMUN $.
      $( [25-Mar-2007] $) $( [18-Mar-2007] $)

    $( Alternate version of Grothendieck's Axiom.  ~ ax-ac is used to derive
       this version. $)
    axgroth3 $p |- E. y ( x e. y /\ A. z e. y ( A. w ( w (_ z -> w e. y ) /\
                       E. w e. y A. v ( v (_ z -> v e. w ) ) /\
                     A. z ( z (_ y -> ( ( y \ z ) ~<_ z \/ z e. y ) ) ) $=
      ( cv wcel wss wi wal wrex wa wral cdif cdom wbr wo w3a wex axgroth2 cuni
      wb com c0 wne visset dominf ne0i sylan infdif2 syl orbi1d imbi2d albidv
      ssid wceq sseq1 elequ1 imbi12d a4b1 mpi r19.22si eluni2 sylibr adantl
      r19.20si dfss3 sylan2 pm5.32i df-3an 3bitr4 exbii mpbir ) AFZBFZGZDFZCFZH
      VQVOGIDJZEFZVRHZVTVQGZIZEJZDVOKZLZCVOMZVRVOHZVOVRNVROPZVRVOGZQZIZCJZRZBSV
      PWGWHVOVROPZWJQZIZCJZRZBSABCDETWNWSBVPWGLZWMLWTWRLWNWSWTWMWRVPVOVOUAZHZWM
      WRUBWGVPXBLZWLWQCXCWKWPWHXCWIWOWJXCUCVOOPZWIWOUBVOUDUEXBXDVPVOBUFZUGVOVNU
      HUIVOVRXECUFUJUKULUMUNWGVRXAGZCVOMXBWFXFCVOWEXFVSWEVRVQGZDVOKXFWDXGDVOWDV
      RVRHZXGVRUOWCXHXGIECVTVRUPWAXHWBXGVTVRVRUQECDURUSUTVAVBDVRVOVCVDVEVFCVOXA
      VGVDVHVIVPWGWMVJVPWGWRVJVKVLVM $.
      $( [31-Mar-2007] $) $( [26-Mar-2007] $)

    $( Alternate version of Grothendieck's Axiom.  ~ ax-ac is used to derive
       this version. $)
    axgroth4 $p |- E. y ( x e. y /\ A. z e. y E. v e. y A. w ( w (_ z ->
    w e. ( y i^i v ) ) /\ A. z ( z (_ y -> ( ( y \ z ) ~<_ z \/ z e. y ) ) ) $=
      ( vu cv wcel wss wi wal wrex wa wral cdif cdom wbr wo w3a wex cin
      axgroth3 wceq elequ2 imbi2d albidv cbvrexv anbi2i r19.42v sseq1 elequ1
      imbi12d cbvalv 19.26 pm4.76 elin imbi2i bitr4 albii 3bitr2 rexbii ralbii
      3anbi2i exbii mpbi ) AGBGZHZDGZCGZIZVHVFHZJZDKZFGZVIIZVNVHHZJZFKZDVFLZMZC
      VFNZVIVFIVFVIOVIPQVIVFHRJCKZSZBTVGVJVHVFEGZUAHZJZDKZEVFLZCVFNZWBSZBTABCDF
      UBWCWJBWAWIVGWBVTWHCVFVTVMVOVNWDHZJZFKZEVFLZMVMWMMZEVFLWHVSWNVMVRWMDEVFVH
      WDUCZVQWLFWPVPWKVODEFUDUEUFUGUHVMWMEVFUIWOWGEVFWOVMVJVHWDHZJZDKZMVLWRMZDK
      WGWMWSVMWLWRFDVNVHUCVOVJWKWQVNVHVIUJFDEUKULUMUHVLWRDUNWTWFDWTVJVKWQMZJWFV
      JVKWQUOWEXAVJVHVFWDUPUQURUSUTVAUTVBVCVDVE $.
      $( [16-Apr-2007] $) $( [16-Apr-2007] $)

    $( Grothendieck's Axiom implies the Axiom of Infinity (in the form of
       ~ omex ).  Note that our proof does not depend on the Axiom of
       Infinity. $)
    grothinf $p |- om e. V $=
      ( vy vx vz vw vv cv wcel wss wi wal wrex wa wral cen wbr wo w3a wex
      ax-groth ssid wceq sseq1 elequ1 imbi12d a4b1 mpi r19.22si adantl
      r19.20si rexbidv df-rex exancom bitr syl6bb cbvralv df-ral sylib anim2i
      3adant3 19.22i ax-mp inf2 inf3 ) AABCBFZAFZGZCFZDFZHVGVEGZICJZEFZVHHZVKVG
      GZIZEJZCVEKZLZDVEMZVHVEHVHVENOVHVEGPIDJZQZARVFVFVDVGGZVILCRZIBJZLZARBADCE
      SVTWDAVFVRWDVSVRWCVFVRVHVGGZCVEKZDVEMZWCVQWFDVEVPWFVJVOWECVEVOVHVHHZWEVHT
      VNWHWEIEDVKVHUAVLWHVMWEVKVHVHUBEDCUCUDUEUFUGUHUIWGWBBVEMWCWFWBDBVEVHVDUAZ
      WFWACVEKZWBWIWEWACVEDBCUCUJWJVIWALCRWBWACVEUKVIWACULUMUNUOWBBVEUPUMUQURUS
      UTVAVBVC $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)
  $}

  ${
    $d x y z w v u t h g f n m k j $.
    $( Lemma for ~ grothprim .  Expand the membership of an unordered pair into
       primitives. $)
    grothprimlem $p |- ( { u , v } e. w <-> E. g ( g e. w /\ A. h
      ( h e. g <-> ( h = u \/ h = v ) ) ) ) $=
      ( cv cpr wcel wceq wo cab wb wal wa wex dfpr2 eleq1i clabel bitr ) CFZBFZ
      GZAFZHEFZTIUDUAIJZEKZUCHDFZUCHUDUGHUELEMNDOUBUFUCETUAPQUEEDUCRS $.
      $( [31-Mar-2007] $) $( [29-Mar-2007] $)

    $( Grothendieck's Axiom ~ ax-groth expanded into set theory primitives
       using 163 symbols.  An open problem is whether a shorter equivalent
       exists (when expanded to primitives). $)
    grothprim $p |- E. y ( x e. y /\ A. z ( ( z e. y -> E. v ( v e. y /\
       A. w ( A. u ( u e. w -> u e. z ) -> ( w e. y /\ w e. v ) ) ) ) /\ E. w
      ( ( w e. z -> w e. y ) -> ( A. v ( ( v e. z -> E. t A. u ( E. g ( g e. w
      /\ A. h ( h e. g <-> ( h = v \/ h = u ) ) ) -> u = t ) ) /\ ( v e. y ->
      ( v e. z \/ E. u ( u e. z /\ E. g ( g e. w /\ A. h ( h e. g <-> ( h = u
      \/ h = v ) ) ) ) ) ) ) \/ z e. y ) ) ) ) $=
      ( cv wcel wss cin wi wal wrex wral cdif cdom wbr wo w3a wex wa wceq wb
      axgroth4 3anass dfss2 elin imbi12i albii rexbii df-rex bitr ralbii
      df-ral cpr wmo cvv visset difexg ax-mp c0 incom difdisj eqtr brdom6disj
      orbi1i ax-17 19.44 bitr4 19.35 grothprimlem mobii mo2 wn impexp eldif
      df-or imbi2i 3bitr4 anbi12i 19.26 exbii 3bitr2 anbi2i mpbi ) AJBJZKZDJZCJ
      ZLZWKWIEJZMKZNZDOZEWIPZCWIQZWLWILZWIWLRZWLSTZWLWIKZUAZNZCOZUBZBUCWJXCWNWI
      KZFJZWKKXIWLKZNFOZWKWIKZWKWNKUDZNZDOZUDEUCZNZWKWLKXLNZWNWLKZHJZWKKZIJZXTK
      ZYBWNUEZYBXIUEZUAUFIOUDHUCZXIGJUENFOGUCZNZXHXSXJYAYCYEYDUAUFIOUDHUCZUDFUC
      ZUAZNZUDEOZXCUAZNZDUCZUDCOZUDZBUCABCDEUGXGYRBXGWJWSXFUDZUDYRWJWSXFUHYSYQW
      JYSXQCOZYPCOZUDYQWSYTXFUUAWSXPCWIQYTWRXPCWIWRXOEWIPXPWQXOEWIWPXNDWMXKWOXM
      FWKWLUIWKWIWNUJUKULUMXOEWIUNUOUPXPCWIUQUOXEYPCXEXRDOZWNXIURWKKZFUSZEWLQZX
      IWNURWKKZFWLPZEXAQZUDZXCUAZDUCZNXRUUJNZDUCYPWTUUBXDUUKDWLWIUIXDUUIDUCZXCU
      AUUKXBUUMXCEFXAWLDWIUTKXAUTKBVAWIWLUTVBVCCVAXAWLMWLXAMVDXAWLVEWLWIVFVGVHV
      IUUIXCDXCDVJVKVLUKXRUUJDVMUULYODUUJYNXRUUIYMXCUUIYHEOZYLEOZUDYMUUEUUNUUHU
      UOUUEYGEWLQUUNUUDYGEWLUUDYFFUSYGUUCYFFDFEHIVNVOYFFGYFGVJVPUOUPYGEWLUQUOUU
      HWNXAKZUUGNZEOUUOUUGEXAUQUUQYLEXHXSVQZUDZYJNXHUURYJNZNUUQYLXHUURYJVRUUPUU
      SUUGYJWNWIWLVSUUGYIFWLPYJUUFYIFWLDEFHIVNUMYIFWLUNUOUKYKUUTXHXSYJVTWAWBULU
      OWCYHYLEWDVLVIWAWEWFULWCXQYPCWDVLWGUOWEWH $.
      $( [16-Apr-2007] $) $( [16-Apr-2007] $)
  $}


