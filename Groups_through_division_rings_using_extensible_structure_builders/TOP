$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
       Groups through division rings using extensible structure builders
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  $c +g $.
  $c Grp $.
  $c 0g $.
  $c -g $.
  $c Abel $.
  $c .r $.
  $c Ring $.
  $c 1r $.

  $( Extend class notation with group operation. $)
  cplusg $a class +g $.

  $( Extend class notation with class of all groups. $)
  cgrp $a class Grp $.

  $( Extend class notation with group identity element. $)
  c0g $a class 0g $.

  $( Extend class notation with inverse of group element. $)
  cminusg $a class -g $.

  $( Extend class notation with class of all Abelian groups. $)
  cabel $a class Abel $.

  $( Extend class notation with ring multiplication. $)
  cmulr $a class .r $.

  $( Extend class notation with class of all rings. $)
  crg $a class Ring $.

  $( Extend class notation with ring unit. $)
  cur $a class 1r $.

  $( Define group operation. $)
  df-plusg $a |- +g = ( g e. _V |-> ( g ` 2 ) ) $.

  ${
    $d g G $.
    plusgval.g $e |- G e. _V $.
    $( Value of the group operation extractor. $)
    plusgval $p |- ( +g ` G ) = ( G ` 2 ) $=
      ( vg cplusg c2 df-plusg strcval ) DCAEBCFG $.
      $( [4-Sep-2011] $)
  $}

  $( Utility theorem: index-independent form of ~ df-plusg . $)
  plusgid $p |- +g = ( f e. _V |-> ( f ` ( +g ` ndx ) ) ) $=
    ( cplusg c2 df-plusg 2nn ndxid ) BACADEF $.
    $( [20-Oct-2012] $)

  ${
    $d a b c e f g h i k m p q r s t u v w x y z $.
    $( Define class of all groups. $)
    df-grp $a |- Grp = { f | E. g E. p ( g = ( Base ` f )
       /\ p = ( +g ` f ) /\ ( A. a e. g A. b e. g A. c e. g ( ( a p b ) e. g
            /\ ( ( a p b ) p c ) = ( a p ( b p c ) ) ) /\
          E. e e. g A. a e. g ( ( e p a ) = a /\
            E. m e. g ( m p a ) = e ) ) ) } $.

    $( Define group identity element. $)
    df-0g $a |- 0g = ( g e. _V |-> ( iota_ e e. ( Base ` g )
      A. x e. ( Base ` g ) ( e ( +g ` g ) x ) = x ) ) $.

    $( Define inverse of group element. $)
    df-minusg $a |- -g = ( g e. _V |-> ( x e. ( Base ` g )
  |-> ( iota_ w e. ( Base ` g ) ( w ( +g ` g ) x ) = ( 0g ` g ) ) ) ) $.

    $( Define class of all Abelian groups. $)
    df-abl $a |- Abel = { g e. Grp | A. a e. ( Base ` g )
      A. b e. ( Base ` g ) ( a ( +g ` g ) b ) = ( b ( +g ` g ) a ) } $.

    $( Define ring multiplication. $)
    df-mulr $a |- .r = ( r e. _V |-> ( r ` 3 ) ) $.

    $( Define class of all rings (with unity). $)
    df-ring $a |- Ring = { f e. Abel |
        E. r E. p E. t ( ( r = ( Base ` f ) /\ p = ( +g ` f )
           /\ t = ( .r ` f ) )
       /\ A. x e. r A. y e. r A. z e. r
           ( ( x t y ) e. r /\ ( ( ( x t y ) t z ) = ( x t ( y t z ) )
            /\ ( x t ( y p z ) ) = ( ( x t y ) p ( x t z ) )
            /\ ( ( x p y ) t z ) = ( ( x t z ) p ( y t z ) ) ) )
        /\ E. u e. r A. x e. r ( ( u t x ) = x /\ ( x t u ) = x ) ) } $.

    $( Define ring unity. $)
    df-ur $a |- 1r = ( r e. _V |-> ( iota_ u e. ( Base ` r )
      A. x e. ( Base ` r ) ( ( u ( .r ` r ) x ) = x
           /\ ( x ( .r ` r ) u ) = x ) ) ) $.
  $}

  $( Utility theorem for constructing a group structure without direct
     reference to component indices. $)
  grphyp $p |- ( Base ` ndx ) =/= ( +g ` ndx ) $=
    ( vf cnx cbs cfv c2 cplusg c1 basendx 1ne2 eqnetri df-plusg 2nn ndxargi
    neeqtrri ) BCDZEBFDOGEHIJFAEAKLMN $.
    $( [20-Oct-2012] $)

  $( Utility theorem: index-independent form of ~ df-mulr . $)
  mulrid $p |- .r = ( f e. _V |-> ( f ` ( .r ` ndx ) ) ) $=
    ( cmulr c3 df-mulr 3nn ndxid ) BACADEF $.
    $( [8-Jun-2013] $)

  ${
    $d g B $.  $d g P $.
    grpbase.b $e |- B e. _V $.
    grpbase.p $e |- P e. _V $.
    grpbase.g $e |- G
               = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( The base set of a constructed group. $)
    grpbase $p |- B = ( Base ` G ) $=
      ( vf cbs cplusg baseid plusgid grphyp str2v1 ) HIGCABGJGKFDELM $.
      $( [19-Oct-2012] $)

    $( The operation of a constructed group. $)
    grpplusg $p |- P = ( +g ` G ) $=
      ( vf cbs cplusg baseid plusgid grphyp str2v2 ) HIGCABGJGKFDELM $.
      $( [19-Oct-2012] $)
  $}

  ${
    $d b p B $.  $d p P $.  $d p G $.
    grpbaseg.g $e |- G
               = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( The base set of a constructed group. $)
    grpbaseg $p |- ( ( B e. V /\ P e. W ) -> B = ( Base ` G ) ) $=
      ( vb vp cv cnx cbs cfv cop cplusg cpr wceq id opeq2 preq1d fveq2d
      eqeq12d preq2d syl6eqr eqeq2d visset eqid grpbase vtocl2g ) GIZJKLZUIMZJN
      LZHIZMZOZKLZPAUJAMZUNOZKLZPACKLZPGHABDEUIAPZUIAUPUSVAQVAUOURKVAUKUQUNUIAU
      JRSTUAUMBPZUSUTAVBURCKVBURUQULBMZOCVBUNVCUQUMBULRUBFUCTUDUIUMUOGUEHUEUOUF
      UGUH $.
      $( [2-Aug-2013] $)

    $( The operation of a constructed group. $)
    grpplusgg $p |- ( ( B e. V /\ P e. W ) -> P = ( +g ` G ) ) $=
      ( vp vb cv cnx cbs cfv cop cplusg cpr wceq opeq2 preq1d fveq2d eqeq2d id
      preq2d syl6eqr eqeq12d visset eqid grpplusg vtocl2g ) GIZJKLZHIZMZJNLZUIM
      ZOZNLZPUIUJAMZUNOZNLZPBCNLZPHGABDEUKAPZUPUSUIVAUOURNVAULUQUNUKAUJQRSTUIBP
      ZUIBUSUTVBUAVBURCNVBURUQUMBMZOCVBUNVCUQUIBUMQUBFUCSUDUKUIUOHUEGUEUOUFUGUH
      $.
      $( [2-Aug-2013] $)
  $}

  ${
    grpstrx.b $e |- B e. _V $.
    grpstrx.p $e |- P e. _V $.
    grpstrx.g $e |- G = { <. 1 , B >. , <. 2 , P >. } $.
    $( Structure scaffold property of an explicitly given group. $)
    $( The base of an explicitly given group. $)
    grpbasex $p |- B = ( Base ` G ) $=
      ( vf cbs c1 c2 cn 1nn elisseti 2nn 1ne2 df-base str2v1x ) HGCIJABDEIKLMJK
      NMOFGPQ $.
      $( [17-Oct-2012] $)

    $( The operation of an explicitly given group. $)
    grpplusgx $p |- P = ( +g ` G ) $=
      ( vf cplusg c1 c2 cn 1nn elisseti 2nn 1ne2 df-plusg str2v2x ) HGCIJABDEIK
      LMJKNMOFGPQ $.
      $( [17-Oct-2012] $)
  $}

  ${
    $d a b c e f g m p B $.  $d f g p G $.  $d a b c e f g m p P $.
    isgrp.b $e |- B = ( Base ` G ) $.
    isgrp.p $e |- P = ( +g ` G ) $.
    $( The predicate "is a group." $)
    isgrp $p |- ( G e. Grp <-> ( G e. _V
            /\ A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( vg vf vp cgrp wcel cv cbs cfv wceq cplusg co wa wral wrex w3a wex cab
      cvv df-grp eleq2i fveq2 syl6eqr eqeq2d 3anbi12d 2exbidv eqid elab4g fvex
      eqeltri eleq2 anbi1d raleqbi1dv rexeq anbi2d rexeqbi1dv anbi12d opreq
      eleq1d opreq1d eqtrd opreq2d eqeq12d ralbidv 2ralbidv eqeq1d rexbidv
      rexralbidv ceqsex2v anbi2i 3anass bitr4i 3bitri ) ENOEKPZLPZQRZSZMPZWDTRZ
      SZFPZGPZWGUAZWCOZWLHPZWGUAZWJWKWNWGUAZWGUAZSZUBZHWCUCZGWCUCZFWCUCZCPZWJWG
      UAZWJSZDPZWJWGUAZXCSZDWCUDZUBZFWCUCZCWCUDZUBZUEZMUFKUFZLUGZOEUHOZWCASZWGB
      SZXMUEZMUFKUFZUBZXQWJWKBUAZAOZYCWNBUAZWJWKWNBUAZBUAZSZUBZHAUCZGAUCFAUCZXC
      WJBUAZWJSZXFWJBUAZXCSZDAUDZUBZFAUCCAUDZUEZNXPECLKDMFGHUIUJXOYALEXPWDESZXN
      XTKMYTWFXRWIXSXMYTWEAWCYTWEEQRZAWDEQUKIULUMYTWHBWGYTWHETRZBWDETUKJULUMUNU
      OXPUPUQYBXQYKYRUBZUBYSYAUUCXQXMWLAOZWRUBZHAUCZGAUCZFAUCZXEXHDAUDZUBZFAUCZ
      CAUDZUBUUCKMABAUUAUHIEQURUSBUUBUHJETURUSXRXBUUHXLUULXAUUGFWCAWTUUFGWCAWSU
      UEHWCAXRWMUUDWRWCAWLUTVAVBVBVBXKUUKCWCAXJUUJFWCAXRXIUUIXEXHDWCAVCVDVBVEVF
      XSUUHYKUULYRXSUUFYJFGAAXSUUEYIHAXSUUDYDWRYHXSWLYCAWJWKWGBVGZVHXSWOYEWQYGX
      SWOWLWNBUAYEWLWNWGBVGXSWLYCWNBUUMVIVJXSWQWJWPBUAYGWJWPWGBVGXSWPYFWJBWKWNW
      GBVGVKVJVLVFVMVNXSUUJYQCFAAXSXEYMUUIYPXSXDYLWJXCWJWGBVGVOXSXHYODAXSXGYNXC
      XFWJWGBVGVOVPVFVQVFVRVSXQYKYRVTWAWB $.
      $( [17-Oct-2012] $)

    $( The predicate "is a group." $)
    isgrp2 $p |- ( G e. Grp <-> ( A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( cgrp wcel cvv cv co wceq wa wral wrex elisset c0 wne rexn0 wn cbs cfv
      fvprc syl5eq necon1ai syl adantl wb wi w3a isgrp pm4.71ri 3anass 3bitr3i
      pm5.32 mpbir pm5.21nii ) EKLZEMLZFNZGNZBOZALVFHNZBOVDVEVGBOBOPQHARGARFARZ
      CNZVDBOVDPDNVDBOVIPDASQFARZCASZQZEKTZVKVCVHVKAUAUBVCVJCAUCVCAUAVCUDAEUEUF
      UAIEUEUGUHUIUJUKVCVBVLULUMVCVBQZVCVLQZULVBVCVHVKUNVNVOABCDEFGHIJUOVBVCVMU
      PVCVHVKUQURVCVBVLUSUTVA $.
      $( [24-Oct-2012] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    elgrp.b $e |- B e. _V $.
    elgrp.p $e |- P e. _V $.
    elgrp.k $e |- K
          = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( The predicate "is a group structure" for a specific structure member. $)
    elgrp $p |- ( K e. Grp <-> ( A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( grpbase grpplusg isgrp2 ) ABCDEFGHABEIJKLABEIJKMN $.
      $( [24-Oct-2012] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    grpprop.b $e |- ( Base ` K ) = B $.
    grpprop.p $e |- ( +g ` K ) = P $.
    grpprop.c $e |- ( Base ` L ) = B $.
    grpprop.q $e |- ( +g ` L ) = P $.
    $( Any set with the same (relevant) properties as a given group is also a
       group (and vice-versa). $)
    grpprop $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm cgrp wcel cv co wceq wa wral wrex cbs cfv eqcomi cplusg
      isgrp2 bitr4i ) CNOIPZJPZBQZAOUJKPZBQUHUIUKBQBQRSKATJATIATLPZUHBQUHRMPUHB
      QULRMAUASIATLAUASDNOABLMCIJKCUBUCAEUDCUEUCBFUDUFABLMDIJKDUBUCAGUDDUEUCBHU
      DUFUG $.
      $( [28-Oct-2012] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    grpstr.b $e |- ( Base ` K ) = B $.
    grpstr.p $e |- ( +g ` K ) = P $.
    grpstr.l $e |- L
          = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( Generalize a specific 2-element group ` L ` to show that any set ` K `
       with the same (relevant) properties is also a group. $)
    grpstr $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm cgrp wcel cv co wceq wa wral wrex cbs cfv eqcomi cplusg
      isgrp2 cvv fvex eqeltrri elgrp bitr4i ) CMNHOZIOZBPZANUMJOZBPUKULUNBPBPQR
      JASIASHASKOZUKBPUKQLOUKBPUOQLATRHASKATRDMNABKLCHIJCUAUBZAEUCCUDUBZBFUCUEA
      BKLDHIJUPAUFECUAUGUHUQBUFFCUDUGUHGUIUJ $.
      $( [28-Oct-2012] $)
  $}

  ${
    $d a b c e f m B $.  $d f G $.  $d a b c e f m P $.  $d a b c X $.
    $d b c Y $.  $d c Z $.
    grplem1.b $e |- B = ( Base ` G ) $.
    grplem1.p $e |- P = ( +g ` G ) $.
    $( Lemma for group properties. $)
    grplem1 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
       ( ( X P Y ) e. B /\ ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) ) $=
      ( va vb vc ve vm cgrp wcel cv co wceq wa wral w3a cvv wrex isgrp simp2bi
      opreq1 eleq1d opreq1d eqeq12d anbi12d opreq2 opreq2d anbi2d rcla43v
      mpan9 ) CNOZIPZJPZBQZAOZUSKPZBQZUQURVABQZBQZRZSZKATJATIATZDAOEAOFAOUADEBQ
      ZAOZVHFBQZDEFBQZBQZRZSZUPCUBOVGLPZUQBQUQRMPUQBQVORMAUCSIATLAUCABLMCIJKGHU
      DUEVFVNDURBQZAOZVPVABQZDVCBQZRZSVIVHVABQZDEVABQZBQZRZSIJKDEFAAAUQDRZUTVQV
      EVTWEUSVPAUQDURBUFZUGWEVBVRVDVSWEUSVPVABWFUHUQDVCBUFUIUJURERZVQVIVTWDWGVP
      VHAUREDBUKZUGWGVRWAVSWCWGVPVHVABWHUHWGVCWBDBUREVABUFULUIUJVAFRZWDVMVIWIWA
      VJWCVLVAFVHBUKWIWBVKDBVAFEBUKULUIUMUNUO $.
      $( [14-Aug-2011] $)

    $( Closure of the operation of a group. $)
    grpcl $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( cgrp wcel co wa w3a 3anass anidm anbi2i bitri wceq grplem1 simpld
      sylan2br 3impb ) CHIZDAIZEAIZDEBJZAIZUCUDKZUBUCUDUDLZUFUHUCUDUDKZKUGUCUDU
      DMUIUDUCUDNOPUBUHKUFUEEBJDEEBJBJQABCDEEFGRSTUA $.
      $( [14-Aug-2011] $)

    $( A group operation is associative. $)
    grpass $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
              ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) $=
      ( cgrp wcel w3a wa co wceq grplem1 simprd ) CIJDAJEAJFAJKLDEBMZAJQFBMDEFB
      MBMNABCDEFGHOP $.
      $( [14-Aug-2011] $)

    $d u x y B $.  $d u x y P $.  $d a b c f u x y $.
    $( A group has a left identity element, and every member has a left
       inverse. $)
    grplidinv $p |- ( G e. Grp ->
         E. u e. B A. x e. B ( ( u P x ) = x /\ E. y e. B ( y P x ) = u ) ) $=
      ( vb vc cgrp wcel cvv cv co wceq wa wral wrex isgrp simp3bi ) FKLFMLANZIN
      ZEOZDLUDJNZEOUBUCUEEOEOPQJDRIDRADRCNZUBEOUBPBNUBEOUFPBDSQADRCDSDECBFAIJGH
      TUA $.
      $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem1 $p |- ( ( ( G e. Grp /\ ( Y e. B /\ A e. B ) ) /\
                  ( ( Y P A ) = U /\ ( A P A ) = A ) ) -> ( U P A ) = U ) $=
      ( cgrp wcel wa co wceq opreq1 ad2antrl w3a id 3anidm23 grpass sylan2
      adantr eqtr3d opreq2 ad2antll simprl 3eqtrd ) EIJZFBJZABJZKZKZFACLZDMZAAC
      LZAMZKZKZDACLZFUNCLZULDUQULACLZURUSUMUTURMUKUOULDACNOUKUTUSMZUPUJUGUHUIUI
      PZVAUHUIVBVBQRBCEFAAGHSTUAUBUOUSULMUKUMUNAFCUCUDUKUMUOUEUF $.
      $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem2 $p |- ( ( ( G e. Grp /\ ( Y e. B /\ A e. B ) ) /\ ( (
 U P Y ) = Y /\ ( Y P A ) = U ) ) -> ( ( A P Y ) P ( A P Y ) ) = ( A P Y ) ) $=
      ( cgrp wcel wa co wceq w3a simprr simprl grpcl 3com23 3expb 3jca grpass
      syldan adantr opreq1 adantl simpl eqtr2d id 3anidm13 sylan2 sylan9eqr
      eqcomd opreq2d eqtrd ) EIJZFBJZABJZKZKZDFCLZFMZFACLZDMZKZKZAFCLZVFCLZAFVF
      CLZCLZVFUSVGVIMZVDUOURUQUPVFBJZNVJUSUQUPVKUOUPUQOUOUPUQPUOUPUQVKUOUQUPVKB
      CEAFGHQRSTBCEAFVFGHUAUBUCVEVHFACVEFVHVDUSFVBFCLZVHVDVLUTFVCVLUTMVAVBDFCUD
      UEVAVCUFUGURUOUPUQUPNZVLVHMUPUQVMVMUHUIBCEFAFGHUAUJUKULUMUN $.
      $( [16-Aug-2011] $)

    ${
      $d w x y A $.  $d w z B $.  $d w y G $.  $d w z P $.  $d w x y z U $.
      $d w y ph $.  $d y ps $.
      grpidinvlem3.ph $e |- ( ph <-> A. x e. B ( U P x ) = x ) $.
      grpidinvlem3.ps $e |- ( ps <-> A. x e. B E. z e. B ( z P x ) = U ) $.
      $( Lemma for ~ grpidinv . $)
      grpidinvlem3 $p |- ( ( ( ( G e. Grp /\ U e. B ) /\ ( ph /\ ps ) )
               /\ A e. B ) -> E. y e. B ( ( y P A ) = U /\ ( A P y ) = U ) ) $=
        ( vw cgrp wcel wa cv co wceq wrex wral opreq1 eqeq1d cbvrexv ralbii
        bitri opreq2 rexbidv rcla4cva sylanb adantll grpcl 3expa adantllr
        biimpi ad2antrl ad2antrr id eqeq12d rcla4va syl11anc adantr pm3.22
        an31s adantlr adantlll anim1i grpidinvlem2 wi 3expb ad2ant2rl sylan2b
        anass an32s ex syldan imp grpidinvlem1 sylan exp43 r19.23adv syl5
        mpand exp32 com34 imp32 exp3a imp31 mpd eqtr3d ancld reximdva ) JPQZIGQ
        ZRZABRZRZFGQZRZDSZFHTZIUAZDGUBZXDFXBHTZIUAZRZDGUBWRWTXEWQBWTXEABXBCSZHT
        ZIUAZDGUBZCGUCZWTXEBESZXIHTZIUAZEGUBZCGUCZXMNXQXLCGXPXKEDGXNXBUAXOXJIXN
        XBXIHUDUEUFUGUHXLXECFGXIFUAZXKXDDGXSXJXCIXIFXBHUIUEUJUKULUMUMXAXDXHDGXA
        XBGQZRZXDXGYAXDXGYAXDRZIXFHTZXFIYAYCXFUAZXDYAXFGQZIXIHTZXIUAZCGUCZYDWQW
        TXTYEWRWOWTXTYEWPWOWTXTYEGHJFXBKLUNZUOUPUPWSYHWTXTAYHWQBAYHMUQURUSYGYDC
        XFGXIXFUAZYFYCXIXFXIXFIHUIYJUTVAVBVCVDYBXFXFHTXFUAZYCIUAZYBWOXTWTRZRZIX
        BHTZXBUAZXDRYKYAYNXDWQWTXTYNWRWOWTXTYNWPXTWTWOYNYMWOVEVFUPUPVDYAYPXDWRW
        TXTYPWQWRXTYPWTAXTYPBAYHXTYPMYGYPCXBGXIXBUAZYFYOXIXBXIXBIHUIYQUTVAUKULV
        GVGVHVIFGHIJXBKLVJVCYAYKYLVKZXDWSWTXTYRWSWTXTYRWQABWTXTRZYRVKWQAYSBYRWQ
        AYSBYRVKWQAYSRRZYEBYRWOYSYEWPAWOWTXTYEYIVLZVMYEBROSZXFHTZIUAZOGUBZYTYRB
        YEUUBXIHTZIUAZOGUBZCGUCZUUEBXRUUINXQUUHCGXPUUGEOGXNUUBUAXOUUFIXNUUBXIHU
        DUEUFUGUHUUHUUECXFGYJUUGUUDOGYJUUFUUCIXIXFUUBHUIUEUJVBVNYTUUDYROGYTUUBG
        QZUUDYKYLYTUUJRWOUUJYERRZUUDYKRYLYTUUJUUKWOYSUUJUUKVKZWPAWOYSYEUULUUAWO
        YERUUJUUKWOUUJYEUUKWOUUJRYERUUKWOUUJYEVOUQVPVQVRVMVSXFGHIJUUBKLVTWAWBWC
        WDWEWFWGWHWIWJVDWKWLVQWMWNWK $.
        $( [16-Aug-2011] $)
    $}

    $d y G $.  $d y U $.  $d y X $.
    $( Lemma for ~ grpidinv . $)
    grpidinvlem4 $p |- ( ( ( G e. Grp /\ X e. B ) /\ E. y e. B
             ( ( y P X ) = U /\ ( X P y ) = U ) ) -> ( X P U ) = ( U P X ) ) $=
      ( cgrp wcel wa cv co wceq wrex simpll simplr simpr grpass syl13anc
      opreq2 sylan9eq opreq1 sylan9req anasss exp31 r19.23adv imp ) EIJZFBJZKZA
      LZFCMZDNZFULCMZDNZKZABOFDCMZDFCMZNZUKUQUTABUKULBJZUQUTUKVAKZUNUPUTVBUNKUP
      URUOFCMZUSVBUNVCFUMCMZURVBUIUJVAUJVCVDNUIUJVAPUIUJVAQZUKVARVEBCEFULFGHSTU
      MDFCUAUBUODFCUCUDUEUFUGUH $.
      $( [16-Aug-2011] $)

    $d w z B $.  $d u x G $.  $d w z P $.  $d u w x y z $.
    $( A group has a left and right identity element, and every member has a
       left and right inverse. $)
    grpidinv $p |- ( G e. Grp -> E. u e. B A. x e. B ( ( ( u P x ) = x /\
         ( x P u ) = x ) /\ E. y e. B ( ( y P x ) = u /\ ( x P y ) = u ) ) ) $=
      ( vz vw cgrp wcel cv co wceq wrex wa wral grplidinv simpl ralimi opreq2
      id eqeq12d rcla4cva sylan adantll anim1i adantrr adantr adantl ad2antlr
      simpr jca32 biid grpidinvlem3 sylancom grpidinvlem4 syl11anc eqtrd jca31
      r19.21aiva exp32 reximdvai mpd ) FKLZCMZIMZENZVHOZJMVHENVGOJDPZQZIDRZCDPV
      GAMZENZVNOZVNVGENZVNOZQBMZVNENVGOVNVSENVGOQBDPZQZADRZCDPIJCDEFGHSVFVMWBCD
      VFVGDLZVMWBVFWCVMQZQZWAADWEVNDLZQZVPVRVTWDWFVPVFVMWFVPWCVMVJIDRZWFVPVLVJI
      DVJVKTUAZVJVPIVNDVHVNOZVIVOVHVNVHVNVGEUBWJUCUDUEUFUGUGZWGVQVOVNWGVFWFQVTV
      QVOOWEVFWFVFWDTUHWEWFVFWCQZWHVKIDRZQQVTWGWLWHWMWEWLWFVFWCWLVMWLUCUIUJWDWH
      VFWFVMWHWCWIUKULWDWMVFWFVMWMWCVLVKIDVJVKUMUAUKULUNWHWMIBJVNDEVGFGHWHUOWMU
      OUPUQZBDEVGFVNGHURUSWKUTWNVAVBVCVDVE $.
      $( [16-Aug-2011] $)

    $d w G $.  $d z G $.
    $( The left identity element of a group is unique.  Lemma 2.2.1(a) of
       [Herstein] p. 55. $)
    grpideu $p |- ( G e. Grp -> E! u e. B A. x e. B ( u P x ) = x ) $=
      ( vw vz vy cgrp wcel cv co wceq wral wi wa wrex wreu grpidinv simpll
      ralimi opreq2 id eqeq12d cbvralv sylib adantl ad2antlr simpr eqeq1d
      opreq1 anbi12d rexbidv rcla4va adantll sylan2 grpidinvlem4 syldan an32s
      adantllr adantr ad2ant2rl ad2ant2lr 3eqtr3d ex mpand r19.21aiva jca
      reximdva mpd ralbidv reu8 sylibr ) EKLZBMZAMZDNZVROZACPZHMZVRDNZVROZACPZV
      QWBOZQZHCPZRZBCSZWABCTVPVQIMZDNZWKOZWKVQDNWKOZRZJMZWKDNZVQOZWKWPDNZVQOZRZ
      JCSZRZICPZBCSWJIJBCDEFGUAVPXDWIBCVPVQCLZRZXDWIXFXDRZWAWHXDWAXFXDWMICPWAXC
      WMICWMWNXBUBUCWMVTIACWKVROZWLVSWKVRWKVRVQDUDXHUEUFUGUHZUIXGWGHCXGWBCLZRZW
      AWEWFXDWAXFXJXIUJXKWAWERZWFXKXLRWBVQDNZVQWBDNZVQWBXKXMXNOZXLVPXDXJXOXEVPX
      JXDXOVPXJRZXDWPWBDNZVQOZWBWPDNZVQOZRZJCSZXOXDXPXBICPZYBXCXBICWOXBUKUCXJYC
      YBVPXBYBIWBCWKWBOZXAYAJCYDWRXRWTXTYDWQXQVQWKWBWPDUDULYDWSXSVQWKWBWPDUMULU
      NUOUPUQURJCDVQEWBFGUSUTVAVBVCXFXJXLXMVQOZXDXFWEYEXJWAXEWEYEVPWDYEAVQCVRVQ
      OZWCXMVRVQVRVQWBDUDYFUEUFUPUQVDVBXJWAXNWBOZXGWEVTYGAWBCVRWBOZVSXNVRWBVRWB
      VQDUDYHUEUFUPVEVFVGVHVIVJVGVKVLWAWEBHCWFVTWDACWFVSWCVRVQWBVRDUMULVMVNVO
      $.
      $( [16-Aug-2011] $)
  $}

  ${
    $d u x y B $.  $d u x y G $.
    grpn0.b $e |- B = ( Base ` G ) $.
    $( The base set of a group is not empty.  (Contributed by Szymon
       Jaroszewicz, 3-Apr-2007.) $)
    grpn0 $p |- ( G e. Grp -> B =/= (/) ) $=
      ( vu vx vy cgrp wcel cv cplusg cfv co wceq wrex wa wral c0 wne eqid
      grplidinv rexn0 syl ) BGHDIZEIZBJKZLUDMFIUDUELUCMFANOEAPZDANAQREFDAUEBCUE
      STUFDAUAUB $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d e m x y z B $.  $d e m x y z G $.  $d m N $.  $d e m P $.  $d e m x Z $.
    $d e m x y z ph $.
    isgrpd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrpd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrpd.g $e |- ( ph -> G e. _V ) $.
    isgrpd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpd.z $e |- ( ph -> Z e. B ) $.
    isgrpd.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    isgrpd.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpd.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Deduce a group from its properties. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpd $p |- ( ph -> G e. Grp ) $=
      ( ve vm cvv wcel cv cplusg cfv co cbs wceq wa wral wrex w3a cgrp wi
      3adant3r3 jca 3exp2 imp3a r19.21adv r19.21aivv opreq1 eqeq1d rcla4ev
      syl11anc r19.21aiva eqeq2 rexbidv anbi12d ralbidv 3jca opreqd eleq12d
      eqidd opreq123d eqeq12d raleqbidv rexeqbidv 3anbi23d mpbid eqid isgrp
      sylibr ) AGUAUBZBUCZCUCZGUDUEZUFZGUGUEZUBZWGDUCZWFUFZWDWEWJWFUFZWFUFZUHZU
      IZDWHUJZCWHUJZBWHUJZSUCZWDWFUFZWDUHZTUCZWDWFUFZWSUHZTWHUKZUIZBWHUJZSWHUKZ
      ULZGUMUBAWCWDWEFUFZEUBZXJWJFUFZWDWEWJFUFZFUFZUHZUIZDEUJZCEUJZBEUJZWSWDFUF
      ZWDUHZXBWDFUFZWSUHZTEUKZUIZBEUJZSEUKZULXIAWCXSYGLAXQBCEEAWDEUBZWEEUBZUIXP
      DEAYHYIWJEUBZXPUNAYHYIYJXPAYHYIYJULUIXKXOAYHYIXKYJMUONUPUQURUSUTAIEUBIWDF
      UFZWDUHZYBIUHZTEUKZUIZBEUJZYGOAYOBEAYHUIZYLYNPYQHEUBHWDFUFZIUHZYNQRYMYSTH
      EXBHUHYBYRIXBHWDFVAVBVCVDUPVEYFYPSIEWSIUHZYEYOBEYTYAYLYDYNYTXTYKWDWSIWDFV
      AVBYTYCYMTEWSIYBVFVGVHVIVCVDVJAXSWRYGXHWCAXRWQBEWHJAXQWPCEWHJAXPWODEWHJAX
      KWIXOWNAXJWGEWHAFWFWDWEKVKZJVLAXLWKXNWMAXJWGWJWJFWFKUUAAWJVMVNAWDWDXMWLFW
      FKAWDVMAFWFWEWJKVKVNVOVHVPVPVPAYFXGSEWHJAYEXFBEWHJAYAXAYDXEAXTWTWDAFWFWSW
      DKVKVBAYCXDTEWHJAYBXCWSAFWFXBWDKVKVBVQVHVPVQVRVSWHWFSTGBCDWHVTWFVTWAWB $.
      $( [6-Jun-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d y z N $.  $d x O $.  $d x y z P $.
    $d x y z ph $.
    isgrprd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrprd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrprd.g $e |- ( ph -> G e. _V ) $.
    isgrprd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrprd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrprd.z $e |- ( ph -> O e. B ) $.
    isgrprd.i $e |- ( ( ph /\ x e. B ) -> ( x P O ) = x ) $.
    isgrprd.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrprd.j $e |- ( ( ph /\ x e. B ) -> ( x P N ) = O ) $.
    $( Deduce a group from its properties.  Same as ~ isgrpd except with right
       identity and inverse. ` N ` (negative) is normally dependent on ` x `
       i.e. read it as ` N ( x ) ` . $)
    isgrprd $p |- ( ph -> G e. Grp ) $=
      ( grplidd grplinvd isgrpd ) ABCDEFGHIJKLMNOABCDEFHIMOPNQRSQABCDEFHIMOPNQR
      TUA $.
      $( [10-Aug-2013] $)
  $}

  ${
    $d e m x y z B $.  $d m N $.  $d e m x y z P $.  $d e m x Z $.
    $d x y z ph $.
    isgrpdOLD.b $e |- B = ( Base ` G ) $.
    isgrpdOLD.p $e |- P = ( +g ` G ) $.
    isgrpdOLD.g $e |- ( ph -> G e. _V ) $.
    isgrpdOLD.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpdOLD.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpdOLD.z $e |- ( ph -> Z e. B ) $.
    isgrpdOLD.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    isgrpdOLD.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpdOLD.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpdOLD $p |- ( ph -> G e. Grp ) $=
      ( ve vm cvv wcel cv co wceq wa wral wrex w3a cgrp wi 3adant3r3 jca 3exp2
      imp3a r19.21adv r19.21aivv opreq1 eqeq1d rcla4ev syl11anc r19.21aiva
      eqeq2 rexbidv anbi12d ralbidv 3jca isgrp sylibr ) AGUAUBZBUCZCUCZFUDZEUBZ
      VMDUCZFUDVKVLVOFUDFUDUEZUFZDEUGZCEUGBEUGZSUCZVKFUDZVKUEZTUCZVKFUDZVTUEZTE
      UHZUFZBEUGZSEUHZUIGUJUBAVJVSWILAVRBCEEAVKEUBZVLEUBZUFVQDEAWJWKVOEUBZVQUKA
      WJWKWLVQAWJWKWLUIUFVNVPAWJWKVNWLMULNUMUNUOUPUQAIEUBIVKFUDZVKUEZWDIUEZTEUH
      ZUFZBEUGZWIOAWQBEAWJUFZWNWPPWSHEUBHVKFUDZIUEZWPQRWOXATHEWCHUEWDWTIWCHVKFU
      RUSUTVAUMVBWHWRSIEVTIUEZWGWQBEXBWBWNWFWPXBWAWMVKVTIVKFURUSXBWEWOTEVTIWDVC
      VDVEVFUTVAVGEFSTGBCDJKVHVI $.
      $( [5-Jun-2013] $)
  $}

  ${
    $d e m x y z B $.  $d m N $.  $d e m x y z P $.  $d e m x Z $.
    isgrpi.g $e |- G e. _V $.
    isgrpi.b $e |- B = ( Base ` G ) $.
    isgrpi.p $e |- P = ( +g ` G ) $.
    isgrpi.c $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpi.a $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpi.z $e |- Z e. B $.
    isgrpi.i $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpi.n $e |- ( x e. B -> N e. B ) $.
    isgrpi.j $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` .  OBSOLETE PROOF. $)
    isgrpiOLD $p |- G e. Grp $=
      ( ve vm cgrp wcel cvv cv co wceq wa wral wrex isgrp w3a 3adant3 jca
      rgen3 opreq1 eqeq1d rcla4ev syl11anc rgen eqeq2 rexbidv anbi12d ralbidv
      mp2an mpbir3an ) FTUAFUBUAAUCZBUCZEUDZDUAZVGCUCZEUDVEVFVIEUDEUDUEZUFZCDUG
      BDUGADUGRUCZVEEUDZVEUEZSUCZVEEUDZVLUEZSDUHZUFZADUGZRDUHZDERSFABCJKUIIVKAB
      CDDDVEDUAZVFDUAZVIDUAZUJVHVJWBWCVHWDLUKMULUMHDUAHVEEUDZVEUEZVPHUEZSDUHZUF
      ZADUGZWANWIADWBWFWHOWBGDUAGVEEUDZHUEZWHPQWGWLSGDVOGUEVPWKHVOGVEEUNUOUPUQU
      LURVTWJRHDVLHUEZVSWIADWMVNWFVRWHWMVMWEVEVLHVEEUNUOWMVQWGSDVLHVPUSUTVAVBUP
      VCVD $.
      $( [3-Sep-2011] $)

    $d x y z G $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpi $p |- G e. Grp $=
      ( wtru cgrp wcel tru cbs cfv wceq a1i cplusg cvv cv co 3adant1 w3a
      adantl isgrpd ax-mp ) RFSTUARABCDEFGHDFUBUCUDRJUEEFUFUCUDRKUEFUGTRIUEAUHZ
      DTZBUHZDTZUOUQEUIZDTRLUJUPURCUHZDTUKUSUTEUIUOUQUTEUIEUIUDRMULHDTRNUEUPHUO
      EUIUOUDROULUPGDTRPULUPGUOEUIHUDRQULUMUN $.
      $( [3-Sep-2011] $)
  $}

  ${
    $d x y z B $.  $d x y z g G $.  $d x y z P $.  $d x Z $.
    isgrpix.a $e |- B e. _V $.
    isgrpix.b $e |- P e. _V $.
    isgrpix.g $e |- G = { <. 1 , B >. , <. 2 , P >. } $.
    isgrpix.2 $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpix.3 $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpix.z $e |- Z e. B $.
    isgrpix.5 $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpix.6 $e |- ( x e. B -> N e. B ) $.
    isgrpix.7 $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group.  Read ` N ` as ` N ( x ) ` . $)
    isgrpix $p |- G e. Grp $=
      ( c1 cop c2 cpr cvv prex eqeltri grpbasex grpplusgx isgrpi ) ABCDEFGHFRDS
      ZTESZUAUBKUHUIUCUDDEFIJKUEDEFIJKUFLMNOPQUG $.
      $( [4-Sep-2011] $)
  $}

  ${
    $d e g x B $.  $d e g x G $.  $d g P $.
    grpidval.b $e |- B = ( Base ` G ) $.
    grpidval.p $e |- P = ( +g ` G ) $.
    grpidval.o $e |- O = ( 0g ` G ) $.
    $( The value of the identity element of a group. $)
    grpidval $p |- ( G e. V
  -> O = ( iota_ e e. B A. x e. B ( e P x ) = x ) ) $=
      ( vg wcel cvv cv co wceq wral crio elisset c0g cfv cplusg cbs fveq2
      syl6eqr opreqd eqeq1d raleqbidv riotaeqbidv df-0g riotaex fvmpt syl5eq
      syl ) EGLEMLZFDNZANZCOZUQPZABQZDBRZPEGSUOFETUAVAJKEUPUQKNZUBUAZOZUQPZAVBU
      CUAZQZDVFRVAMTVBEPZVGUTDVFBVHVFEUCUABVBEUCUDHUEZVHVEUSAVFBVIVHVDURUQVHVCC
      UPUQVHVCEUBUACVBEUBUDIUEUFUGUHUIADKUJUTDBUKULUMUN $.
      $( [7-Aug-2013] $) $( [20-Aug-2011] $)
  $}

  ${
    $d o x B $.  $d o x G $.
    grpidcl.b $e |- B = ( Base ` G ) $.
    grpidcl.o $e |- O = ( 0g ` G ) $.
    $( The identity element of a group belongs to the group. $)
    grpidcl $p |- ( G e. Grp -> O e. B ) $=
      ( vo vx cgrp wcel cv cplusg cfv co wceq wral wreu eqid grpideu wa crio
      grpidval adantr riotacl adantl eqeltrd mpdan ) BHIZFJGJZBKLZMUHNGAOZFAPZC
      AIGFAUIBDUIQZRUGUKSCUJFATZAUGCUMNUKGAUIFBCHDULEUAUBUKUMAIUGUJFAUCUDUEUF
      $.
      $( [27-Aug-2011] $)
  $}

  ${
    $d x y X $.  $d u w x y B $.  $d u x y G $.  $d u w x y P $.
    $d u w x y O $.
    grpidinv2.b $e |- B = ( Base ` G ) $.
    grpidinv2.p $e |- P = ( +g ` G ) $.
    grpidinv2.o $e |- O = ( 0g ` G ) $.
    $( X group's properties using the explicit identity element. $)
    grpidinv2 $p |- ( ( G e. Grp /\ X e. B ) -> ( ( ( O P X ) = X /\
         ( X P O ) = X ) /\ E. y e. B ( ( y P X ) = O /\ ( X P y ) = O ) ) ) $=
      ( vx vu vw cgrp wcel cv co wceq wa wrex wral crio wreu grpidinv grpideu
      wss wi ssid simpll ralimi a1i rgen riotass2 mpanl12 syl11anc grpidval
      eqtr4d wb grpidcl reuss2 ax-17 a17d opreq1 eqeq1d opreq2 anbi12d eqeq2
      rexbidv ralbidv riota2f mpbird id eqeq12d rcla4cva sylan ) DMNZEJOZCPZVPQ
      ZVPECPZVPQZRZAOZVPCPZEQZVPWBCPZEQZRZABSZRZJBTZFBNEFCPZFQZFECPZFQZRZWBFCPZ
      EQZFWBCPZEQZRZABSZRZVOWJKOZVPCPZVPQZVPXCCPZVPQZRZWCXCQZWEXCQZRZABSZRZJBTZ
      KBUAZEQZVOXOXEJBTZKBUAZEVOXNKBSZXQKBUBZXOXRQZJAKBCDGHUCZJKBCDGHUDZBBUEZXN
      XQUFZKBTZXSXTRZYABUGZYEKBYEXCBNXMXEJBXEXGXLUHUIUJUKZXNXQKBBULUMUNJBCKDEMG
      HIUOUPVOEBNZXNKBUBZWJXPUQBDEGIURVOXSXTYKYBYCYDYFYGYKYHYIXNXQKBBUSUMUNXNWJ
      KLBELOENKUTYJWJKVAXCEQZXMWIJBYLXHWAXLWHYLXEVRXGVTYLXDVQVPXCEVPCVBVCYLXFVS
      VPXCEVPCVDVCVEYLXKWGABYLXIWDXJWFXCEWCVFXCEWEVFVEVGVEVHVIUNVJWIXBJFBVPFQZW
      AWOWHXAYMVRWLVTWNYMVQWKVPFVPFECVDYMVKZVLYMVSWMVPFVPFECVBYNVLVEYMWGWTABYMW
      DWQWFWSYMWCWPEVPFWBCVDVCYMWEWREVPFWBCVBVCVEVGVEVMVN $.
      $( [18-Aug-2011] $)

    $( The identity element of a group is a left identity. $)
    grplid $p |- ( ( G e. Grp /\ X e. B ) -> ( O P X ) = X ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2 simpll syl ) CJKEAKLDEBMENZED
      BMENZLIOZEBMDNEUBBMDNLIAPZLTIABCDEFGHQTUAUCRS $.
      $( [18-Aug-2011] $)

    $( The identity element of a group is a right identity. $)
    grprid $p |- ( ( G e. Grp /\ X e. B ) -> ( X P O ) = X ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2 simplr syl ) CJKEAKLDEBMENZED
      BMENZLIOZEBMDNEUBBMDNLIAPZLUAIABCDEFGHQTUAUCRS $.
      $( [18-Aug-2011] $)

    $d e B $.  $d e G $.  $d e P $.  $d e x w Z $.
    $( Properties showing that an element ` Z ` is the identity element of a
       group.  See also ~ isgrpid2 . $)
    isgrpid $p |- ( G e. Grp
  -> ( ( Z e. B /\ A. x e. B ( Z P x ) = x ) <-> O = Z ) ) $=
      ( ve vw cgrp wcel cv co wceq wral wa crio grpidval adantr wreu wb
      grpideu anim2i ancoms ax-17 a17d opreq1 eqeq1d ralbidv riota2f syl
      biimpd impr eqtrd ex grpidcl eleq1 syl5ibcom grplid r19.21aiva jcad
      impbid ) DLMZFBMZFANZCOZVGPZABQZRZEFPZVEVKVLVEVKREJNZVGCOZVGPZABQZJBSZFVE
      EVQPVKABCJDELGHITUAVEVFVJVQFPZVEVFRZVJVRVSVFVPJBUBZRZVJVRUCVFVEWAVEVTVFAJ
      BCDGHUDUEUFVPVJJKBFKNFMJUGVFVJJUHVMFPZVOVIABWBVNVHVGVMFVGCUIUJUKULUMUNUOU
      PUQVEVLVFVJVEEBMVLVFBDEGIUREFBUSUTVEEVGCOZVGPZABQVLVJVEWDABBCDEVGGHIVAVBV
      LWDVIABVLWCVHVGEFVGCUIUJUKUTVCVD $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.  $d y Y $.  $d y Z $.
    grprcan.b $e |- B = ( Base ` G ) $.
    grprcan.p $e |- P = ( +g ` G ) $.
    $( Right cancellation law for groups. $)
    grprcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( X P Z ) = ( Y P Z ) <-> X = Y ) ) $=
      ( vy cgrp wcel co wceq wb wa cv c0g cfv wrex wi eqid grpidinv2 simpr
      reximi adantl syl ad2ant2rl opreq1 ad2antll grpass 3exp2 imp41 adantlrl
      adantrr imp42 adantllr 3eqtr3d adantrrl opreq2 ad2antrl grprid ad2antrr
      ad2ant2r adantr exp45 r19.23adv mpd impbid1 exp43 3imp2 ) CJKZDAKZEAKZFAK
      ZDFBLZEFBLZMZDEMZNZVKVLVMVNVSVKVLOZVMVNOZOZVQVRWBFIPZBLZCQRZMZIASZVQVRTZV
      KVNWGVLVMVKVNOWEFBLFMFWEBLFMOZWCFBLWEMZWFOZIASZOWGIABCWEFGHWEUAZUBWLWGWIW
      KWFIAWJWFUCUDUEUFUGWBWFWHIAWBWCAKZWFVQVRWBWNWFVQOOZOZDWEBLZEWEBLZDEWPDWDB
      LZEWDBLZWQWRWBWNVQWSWTMWFWBWNVQOOVOWCBLZVPWCBLZWSWTVQXAXBMWBWNVOVPWCBUHUI
      WBWNXAWSMZVQVTVNWNXCVMVKVLVNWNXCVKVLVNWNXCABCDFWCGHUJUKULUMUNWBWNXBWTMZVQ
      VKWAWNXDVLVKVMVNWNXDVKVMVNWNXDABCEFWCGHUJUKUOUPUNUQURWOWSWQMZWBWFXEWNVQWD
      WEDBUSUTUEWOWTWRMZWBWFXFWNVQWDWEEBUSUTUEUQVTWQDMWAWOABCWEDGHWMVAVBWBWREMZ
      WOVKVMXGVLVNABCWEEGHWMVAVCVDUQVEVFVGDEFBUHVHVIVJ $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d y z B $.  $d y z G $.  $d y z P $.  $d y z O $.  $d y z X $.
    grpinveu.b $e |- B = ( Base ` G ) $.
    grpinveu.p $e |- P = ( +g ` G ) $.
    grpinveu.o $e |- O = ( 0g ` G ) $.
    $( The left inverse element of a group is unique.  Lemma 2.2.1(b) of
       [Herstein] p. 55. $)
    grpinveu $p |- ( ( G e. Grp /\ X e. B )
           -> E! y e. B ( y P X ) = O ) $=
      ( vz cgrp wcel wa cv co wceq wi wral wrex wreu grpidinv2 simpl reximi
      adantl syl w3a eqtr3 grprcan syl5ib 3exp2 com24 imp41 an32s exp3a
      r19.21adva ancld reximdva mpd opreq1 eqeq1d reu8 sylibr ) DKLZFBLZMZANZFC
      OZEPZJNZFCOZEPZVFVIPZQZJBRZMZABSZVHABTVEVHABSZVPVEEFCOFPFECOFPMZVHFVFCOEP
      ZMZABSZMVQABCDEFGHIUAWAVQVRVTVHABVHVSUBUCUDUEVEVHVOABVEVFBLZMZVHVNWCVHVMJ
      BWCVIBLZMVHVKVLVEWDWBVHVKMZVLQZVCVDWDWBWFVCWBWDVDWFVCWBWDVDWFWEVGVJPVCWBW
      DVDUFMVLVGVJEUGBCDVFVIFGHUHUIUJUKULUMUNUOUPUQURVHVKAJBVLVGVJEVFVIFCUSUTVA
      VB $.
      $( [24-Aug-2011] $)

    $( Two ways of saying that an element of a group is the identity
       element. $)
    grpid $p |- ( ( G e. Grp /\ X e. B ) ->
                  ( X = O <-> ( X P X ) = X ) ) $=
      ( cgrp wcel wa co wceq wb wi grpidcl grprcan 3exp2 mpid pm2.43d imp
      grplid eqeq2d bitr3d ) CIJZEAJZKZEEBLZDEBLZMZEDMZUHEMUEUFUJUKNZUEUFULUEUF
      DAJZUFULOACDFHPUEUFUMUFULABCEDEFGQRSTUAUGUIEUHABCDEFGHUBUCUD $.
      $( [24-Aug-2011] $)

    $( Properties showing that an element ` Z ` is the identity element of a
       group. $)
    isgrpid2 $p |- ( G e. Grp
  -> ( ( Z e. B /\ ( Z P Z ) = Z ) <-> O = Z ) ) $=
      ( cgrp wcel co wceq wa simprr wb grpid adantrr mpbird eqcomd ex grpidcl
      grplid mpdan jca eleq1 id opreq12d eqeq12d anbi12d syl5ibcom impbid ) CIJ
      ZEAJZEEBKZELZMZDELZULUPUQULUPMZEDUREDLZUOULUMUONULUMUSUOOUOABCDEFGHPQRSTU
      LDAJZDDBKZDLZMUQUPULUTVBACDFHUAZULUTVBVCABCDDFGHUBUCUDUQUTUMVBUODEAUEUQVA
      UNDEUQDEDEBUQUFZVDUGVDUHUIUJUK $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g O $.  $d g P $.
    grpinvval.b $e |- B = ( Base ` G ) $.
    grpinvval.p $e |- P = ( +g ` G ) $.
    grpinvval.o $e |- O = ( 0g ` G ) $.
    grpinvval.n $e |- N = ( -g ` G ) $.
    $( The inverse function of a group. $)
    grpinvfval $p |- ( G e. V
              -> N = ( x e. B |-> ( iota_ y e. B ( y P x ) = O ) ) ) $=
      ( vg wcel cvv cv co wceq crio cmpt elisset cminusg cfv cbs cplusg c0g
      fveq2 syl6eqr opreqd eqeq12d riotaeqbidv mpteq12dv df-minusg fvex
      eqeltri mptexg ax-mp fvmpt syl5eq syl ) EHNEONZFACBPZAPZDQZGRZBCSZTZREHUA
      VAFEUBUCVGLMEAMPZUDUCZVBVCVHUEUCZQZVHUFUCZRZBVISZTVGOUBVHERZAVIVNCVFVOVIE
      UDUCZCVHEUDUGIUHZVOVMVEBVICVQVOVKVDVLGVOVJDVBVCVOVJEUEUCDVHEUEUGJUHUIVOVL
      EUFUCGVHEUFUGKUHUJUKULABMUMCONVGONCVPOIEUDUNUOACVFOUPUQURUSUT $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)

    $d x O $.  $d x P $.  $d x y X $.
    $( The inverse of a group element. $)
    grpinvval $p |- ( ( G e. V /\ X e. B ) ->
                    ( N ` X ) = ( iota_ y e. B ( y P X ) = O ) ) $=
      ( vx wcel cfv cv co wceq crio cmpt grpinvfval fveq1d opreq2 eqeq1d
      riotabidv eqid riotaex fvmpt sylan9eq ) DGNZHBNHEOHMBAPZMPZCQZFRZABSZTZOU
      KHCQZFRZABSZUJHEUPMABCDEFGIJKLUAUBMHUOUSBUPULHRZUNURABUTUMUQFULHUKCUCUDUE
      UPUFURABUGUHUI $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.
    grpinvcl.b $e |- B = ( Base ` G ) $.
    grpinvcl.p $e |- P = ( +g ` G ) $.
    grpinvcl.n $e |- N = ( -g ` G ) $.
    $( X group element's inverse is a group element. $)
    grpinvcl $p |- ( ( G e. Grp /\ X e. B ) -> ( N ` X ) e. B ) $=
      ( vy cgrp wcel wa cfv cv co c0g wceq crio eqid grpinvval wreu grpinveu
      riotacl syl eqeltrd ) CJKEAKLZEDMINEBOCPMZQZIARZAIABCDUGJEFGUGSZHTUFUHIAU
      AUIAKIABCUGEFGUJUBUHIAUCUDUE $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d w y B $.  $d y G $.  $d w y N $.  $d w y P $.  $d w y O $.  $d w y X $.
    grpinv.b $e |- B = ( Base ` G ) $.
    grpinv.p $e |- P = ( +g ` G ) $.
    grpinv.u $e |- O = ( 0g ` G ) $.
    grpinv.n $e |- N = ( -g ` G ) $.
    $( The properties of a group element's inverse. $)
    grpinv $p |- ( ( G e. Grp /\ X e. B ) ->
                 ( ( ( N ` X ) P X ) = O /\ ( X P ( N ` X ) ) = O ) ) $=
      ( vy vw cgrp wcel wa cfv co wceq cv crio wrex wreu grpidinv2 simprd
      grpinveu wss wi wral ssid simpl a1i rgen riotass2 mpanl12 syl11anc
      grpinvval eqtr4d wb grpinvcl reuss2 ax-17 a17d opreq1 eqeq1d opreq2
      anbi12d riota2f mpbird ) CMNFANOZFDPZFBQZERZFVJBQZERZOZKSZFBQZERZFVPBQZER
      ZOZKATZVJRZVIWBVRKATZVJVIWAKAUAZVRKAUBZWBWDRZVIEFBQFRFEBQFROWEKABCEFGHIUC
      UDZKABCEFGHIUEZAAUFZWAVRUGZKAUHZWEWFOZWGAUIZWKKAWKVPANVRVTUJUKULZWAVRKAAU
      MUNUOKABCDEMFGHIJUPUQVIVJANZWAKAUBZVOWCURABCDFGHJUSVIWEWFWQWHWIWJWLWMWQWN
      WOWAVRKAAUTUNUOWAVOKLAVJLSVJNKVAWPVOKVBVPVJRZVRVLVTVNWRVQVKEVPVJFBVCVDWRV
      SVMEVPVJFBVEVDVFVGUOVH $.
      $( [24-Aug-2011] $)

    $( The left inverse of a group element. $)
    grplinv $p |- ( ( G e. Grp /\ X e. B ) -> ( ( N ` X ) P X ) = O ) $=
      ( cgrp wcel wa cfv co wceq grpinv simpld ) CKLFALMFDNZFBOEPFSBOEPABCDEFGH
      IJQR $.
      $( [24-Aug-2011] $)

    $( The right inverse of a group element. $)
    grprinv $p |- ( ( G e. Grp /\ X e. B ) -> ( X P ( N ` X ) ) = O ) $=
      ( cgrp wcel wa cfv co wceq grpinv simprd ) CKLFALMFDNZFBOEPFSBOEPABCDEFGH
      IJQR $.
      $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid1 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( X P Y ) = O ) ) $=
      ( cgrp wcel w3a cfv wceq co wa opreq2 adantl grprinv 3adant3 adantr
      eqtr3d grplinv opreq1d grpinvcl adantrr simprl simprr 3jca grpass syldan
      3impb grplid 3adant2 3eqtr3d grprid 3eqtr3rd impbida ) CLMZFAMZGAMZNZFDOZ
      GPZFGBQZEPZVDVFRFVEBQZVGEVFVIVGPVDVEGFBSTVDVIEPZVFVAVBVJVCABCDEFHIJKUAUBU
      CUDVDVHRVEVGBQZVEEBQZGVEVHVKVLPVDVGEVEBSTVDVKGPVHVDVEFBQZGBQZEGBQZVKGVAVB
      VNVOPVCVAVBRVMEGBABCDEFHIJKUEUFUBVAVBVCVNVKPZVAVBVCRZVEAMZVBVCNVPVAVQRVRV
      BVCVAVBVRVCABCDFHIKUGZUHVAVBVCUIVAVBVCUJUKABCVEFGHIULUMUNVAVCVOGPVBABCEGH
      IJUOUPUQUCVDVLVEPZVHVAVBVTVCVAVBVRVTVSABCEVEHIJURUMUBUCUSUT $.
      $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid2 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( Y P X ) = O ) ) $=
      ( cgrp wcel w3a cfv wceq co wa opreq1 adantl grplinv 3adant3 adantr
      eqtr3d grpinvcl grplid syldan eqcomd simprr simprl adantrr 3jca grpass
      3impb grprinv opreq2d grprid 3adant2 3eqtrd 3eqtr2d impbida ) CLMZFAMZGAM
      ZNZFDOZGPZGFBQZEPZVEVGRVFFBQZVHEVGVJVHPVEVFGFBSTVEVJEPZVGVBVCVKVDABCDEFHI
      JKUAUBUCUDVEVIRVFEVFBQZVHVFBQZGVEVFVLPVIVEVLVFVBVCVLVFPZVDVBVCVFAMZVNABCD
      FHIKUEZABCEVFHIJUFUGUBUHUCVIVMVLPVEVHEVFBSTVEVMGPVIVEVMGFVFBQZBQZGEBQZGVB
      VCVDVMVRPZVBVCVDRZVDVCVONVTVBWARVDVCVOVBVCVDUIVBVCVDUJVBVCVOVDVPUKULABCGF
      VFHIUMUGUNVBVCVRVSPVDVBVCRVQEGBABCDEFHIJKUOUPUBVBVDVSGPVCABCEGHIJUQURUSUC
      UTVA $.
      $( [24-Aug-2011] $)

    $d e w x B $.  $d e x G $.  $d e w x M $.  $d x N $.  $d e w x O $.
    $d e w x P $.
    $( Properties showing that a function ` M ` is the inverse function of a
       group. $)
    isgrpinv $p |- ( G e. Grp
  -> ( ( M : B --> B /\ A. x e. B ( ( M ` x ) P x ) = O ) <-> N = M ) ) $=
      ( ve vw cgrp wcel wf cv cfv co wceq wral wa crio simplll simplr
      grpinvval syl11anc simpr wreu wb simpllr ffvelrn grpinveu ax-17 a17d
      opreq1 eqeq1d riota2f mpbid eqtrd ex ralimdva impr wfn cmpt cvv eqid
      fnmpt riotaex a1i mprg grpinvfval fneq1d adantr mpbiri ffn ad2antrl
      eqfnfv2 mpbird grpinvcl eqeltrrd fmptd feq1d grplinv r19.21aiva jca feq1
      fveq1 opreq1d ralbidv anbi12d syl5ibcom impbid ) DNOZBBEPZAQZERZWPCSZGTZA
      BUAZUBZFETZWNXAXBWNXAUBZXBWPFRZWQTZABUAZWNWOWTXFWNWOUBZWSXEABXGWPBOZUBZWS
      XEXIWSUBZXDLQZWPCSZGTZLBUCZWQXJWNXHXDXNTWNWOXHWSUDZXGXHWSUEZLBCDFGNWPHIJK
      UFZUGXJWSXNWQTZXIWSUHXJWQBOZXMLBUIZWSXRUJXJWOXHXSWNWOXHWSUKXPBBWPEULUGXJW
      NXHXTXOXPLBCDGWPHIJUMUGXMWSLMBWQMQWQOLUNXSWSLUOXKWQTXLWRGXKWQWPCUPUQURUGU
      SUTVAVBVCXCFBVDZEBVDZXBXFUJXCYAABXNVEZBVDZXNVFOZYDABABXNYCVFYCVGZVHYEXHXM
      LBVIVJVKWNYAYDUJXAWNBFYCALBCDFGNHIJKVLZVMVNVOWOYBWNWTBBEVPVQABFEVRUGVSVAW
      NBBFPZXDWPCSZGTZABUAZUBXBXAWNYHYKWNYHBBYCPWNABXNBYCWNXHXNBOWNXHUBXDXNBXQB
      CDFWPHIKVTWAVAYFWBWNBBFYCYGWCVSWNYJABBCDFGWPHIJKWDWEWFXBYHWOYKWTBBFEWGXBY
      JWSABXBYIWRGXBXDWQWPCWPFEWHWIUQWJWKWLWM $.
      $( [7-Aug-2013] $)
  $}

  ${
    grpinvid.u $e |- O = ( 0g ` G ) $.
    grpinvid.n $e |- N = ( -g ` G ) $.
    $( The inverse of the identity element of a group. $)
    grpinvid $p |- ( G e. Grp -> ( N ` O ) = O ) $=
      ( cgrp wcel cfv wceq cplusg co cbs eqid grpidcl grplid mpdan wb
      grpinvid1 mpd3an23 mpbird ) AFGZCBHCIZCCAJHZKCIZUACALHZGZUDUEACUEMZDNZUEU
      CACCUGUCMZDOPUAUFUFUBUDQUHUHUEUCABCCCUGUIDERST $.
      $( [24-Aug-2011] $)
  $}

  ${
    grplcan.b $e |- B = ( Base ` G ) $.
    grplcan.p $e |- P = ( +g ` G ) $.
    $( Left cancellation law for groups. $)
    grplcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( Z P X ) = ( Z P Y ) <-> X = Y ) ) $=
      ( cgrp wcel w3a wa co wceq wi cminusg cfv opreq2 adantl c0g eqid grplinv
      adantlr opreq1d grpinvcl adantrl simprr simprl 3jca grpass syldan
      anassrs grplid adantr 3eqtr3d adantrr exp53 3imp2 impbid1 ) CIJZDAJZEAJZF
      AJZKLFDBMZFEBMZNZDENZUTVAVBVCVFVGOUTVAVBVCVFVGUTVALZVBVCLZLZVFLFCPQZQZVDB
      MZVLVEBMZDEVFVMVNNVJVDVEVLBRSVJVMDNZVFVHVCVOVBVHVCLZVLFBMZDBMZCTQZDBMZVMD
      VPVQVSDBUTVCVQVSNZVAABCVKVSFGHVSUAZVKUAZUBZUCUDUTVAVCVRVMNZUTVAVCLZVLAJZV
      CVAKWEUTWFLWGVCVAUTVCWGVAABCVKFGHWCUEZUFUTVAVCUGUTVAVCUHUIABCVLFDGHUJUKUL
      VHVTDNVCABCVSDGHWBUMUNUOUFUNVJVNENZVFUTVIWIVAUTVILZVQEBMZVSEBMZVNEWJVQVSE
      BUTVCWAVBWDUFUDUTVIWGVCVBKWKVNNWJWGVCVBUTVCWGVBWHUFUTVBVCUGUTVBVCUHUIABCV
      LFEGHUJUKUTVBWLENVCABCVSEGHWBUMUPUOUCUNUOUQURDEFBRUS $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g P $.
    isabl.b $e |- B = ( Base ` G ) $.
    isabl.p $e |- P = ( +g ` G ) $.
    $( The predicate "is an Abelian (commutative) group." $)
    isabl $p |- ( G e. Abel <-> ( G e. Grp /\
             A. x e. B A. y e. B ( x P y ) = ( y P x ) ) ) $=
      ( vg cv cplusg cfv co wceq cbs wral cgrp cabel wb fveq2 syl6eqr raleq
      raleqbi1dv syl opreqd eqeq12d 2ralbidv bitrd df-abl elrab2 ) AIZBIZHIZJKZ
      LZUKUJUMLZMZBULNKZOZAUQOZUJUKDLZUKUJDLZMZBCOACOZHEPQULEMZUSUPBCOZACOZVCVD
      UQCMUSVFRVDUQENKCULENSFTURVEAUQCUPBUQCUAUBUCVDUPVBABCCVDUNUTUOVAVDUMDUJUK
      VDUMEJKDULEJSGTZUDVDUMDUKUJVGUDUEUFUGHABUHUI $.
      $( [17-Oct-2011] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d x y ph $.
    isabld.g $e |- ( ph -> G e. Grp ) $.
    isabld.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isabld.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isabld.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) = ( y P x ) ) $.
    $( Properties that determine an Abelian group. $)
    isabld $p |- ( ph -> G e. Abel ) $=
      ( cgrp wcel cv cplusg cfv co wceq cbs wral wa cabel opreqd eqeq12d
      raleqbidv anbi2d 3expib r19.21aivv mpbi2and eqid isabl sylibr ) AFKLZBMZC
      MZFNOZPZUNUMUOPZQZCFROZSZBUSSZTZFUALAULUMUNEPZUNUMEPZQZCDSZBDSZVBAVGVAULA
      VFUTBDUSHAVEURCDUSHAVCUPVDUQAEUOUMUNIUBAEUOUNUMIUBUCUDUDUEGAVEBCDDAUMDLUN
      DLVEJUFUGUHBCUSUOFUSUIUOUIUJUK $.
      $( [6-Aug-2013] $)
  $}

  ${
    $d x y B $.  $d x y G $.
    isabli.g $e |- G e. Grp $.
    isabli.b $e |- B = ( Base ` G ) $.
    isabli.p $e |- P = ( +g ` G ) $.
    isabli.c $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) = ( y P x ) ) $.
    $( Properties that determine an Abelian group. $)
    isabli $p |- G e. Abel $=
      ( cabel wcel cgrp cv co wceq wral isabl rgen2a mpbir2an ) EJKELKAMZBMZDNU
      ATDNOZBCPACPABCDEGHQFUBABCIRS $.
      $( [4-Sep-2011] $)
  $}

  ${
    $d x y G $.
    $( An Abelian group is a group. $)
    ablgrp $p |- ( G e. Abel -> G e. Grp ) $=
      ( vx vy cabel wcel cgrp cv cplusg cfv co wceq cbs wral eqid isabl
      simplbi ) ADEAFEBGZCGZAHIZJRQSJKCALIZMBTMBCTSATNSNOP $.
      $( [26-Aug-2011] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d x y P $.  $d x y X $.  $d y Y $.
    ablcom.b $e |- B = ( Base ` G ) $.
    ablcom.p $e |- P = ( +g ` G ) $.
    $( An Abelian group operation is commutative. $)
    ablcom $p |- ( ( G e. Abel /\ X e. B /\ Y e. B ) ->
                 ( X P Y ) = ( Y P X ) ) $=
      ( vx vy cabel wcel co wceq cv wral wa cgrp isabl simprbi opreq1 opreq2
      eqeq12d rcla42v syl5com 3impib ) CJKZDAKZEAKZDEBLZEDBLZMZUFHNZINZBLZUMULB
      LZMZIAOHAOZUGUHPUKUFCQKUQHIABCFGRSUPUKDUMBLZUMDBLZMHIDEAAULDMUNURUOUSULDU
      MBTULDUMBUAUBUMEMURUIUSUJUMEDBUAUMEDBTUBUCUDUE $.
      $( [26-Aug-2011] $)
  $}

  ${
    $d x y z G $.
    cnaddablx.g $e |- G = { <. 1 , CC >. , <. 2 , + >. } $.
    $( The complex numbers are an Abelian group under addition.  This version
       of ~ cnaddabl shows the explicit structure "scaffold" we chose for the
       definition for Abelian groups. $)
    cnaddablx $p |- G e. Abel $=
      ( vx vy vz cc caddc cv cneg cc0 ax-cnex addex ax-addcl ax-addass 0cn
      addid2 negcl wcel co wceq addcom mpdan negid eqtr3d isgrpix grpbasex
      grpplusgx isabli ) CDFGACDEFGACHZIZJKLBUIDHZMUIUKEHNOUIPUIQZUIFRZUIUJGSZU
      JUIGSZJUMUJFRUNUOTULUIUJUAUBUIUCUDUEFGAKLBUFFGAKLBUGUIUKUAUH $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d x y z G $.
    cnaddabl.g $e |- G
               = { <. ( Base ` ndx ) , CC >. , <. ( +g ` ndx ) , + >. } $.
    $( The complex numbers are an Abelian group under addition.  This version
       of ~ cnaddablx hides the explicit structure indices i.e. is
       "scaffold-independent".  Note that the proof also does not reference
       explicit structure indices.  The actual structure is dependent on how
       ` Base ` and ` +g ` is defined. $)
    cnaddabl $p |- G e. Abel $=
      ( vx vy vz cc caddc cv cneg cc0 cnx cbs cfv cop cplusg cpr cvv prex
      eqeltri ax-cnex addex grpbase grpplusg ax-addcl ax-addass 0cn addid2
      negcl wcel co wceq addcom mpdan negid eqtr3d isgrpi isabli ) CDFGACDEFGAC
      HZIZJAKLMFNZKOMGNZPQBUTVARSFGATUABUBZFGATUABUCZURDHZUDURVDEHUEUFURUGURUHZ
      URFUIZURUSGUJZUSURGUJZJVFUSFUIVGVHUKVEURUSULUMURUNUOUPVBVCURVDULUQ $.
      $( [20-Oct-2012] $)
  $}

  ${
    $d x y z G $.
    zaddablx.g $e |- G = { <. 1 , ZZ >. , <. 2 , + >. } $.
    $( The integers are an Abelian group under addition. $)
    zaddablx $p |- G e. Abel $=
      ( vx vy vz cz caddc cv cneg cc0 zex addex zaddcl wcel cc co wceq zcn
      addass syl3an 0z addid2 syl znegcl addcom syl2an mpdan negid eqtr3d
      isgrpix grpbasex grpplusgx isabli ) CDFGACDEFGACHZIZJKLBUNDHZMUNFNZUNONZU
      PFNZUPONZEHZFNVAONUNUPGPZVAGPUNUPVAGPGPQUNRZUPRZVARUNUPVASTUAUQURJUNGPUNQ
      VCUNUBUCUNUDZUQUNUOGPZUOUNGPZJUQUOFNZVFVGQZVEUQURUOONVIVHVCUORUNUOUEUFUGU
      QURVFJQVCUNUHUCUIUJFGAKLBUKFGAKLBULUQURUTVBUPUNGPQUSVCVDUNUPUEUFUM $.
      $( [4-Sep-2011] $)
  $}

  $( Utility theorem for constructing a ring (or generally, 3-element)
     structure without direct reference to component indices.  This theorem
     extends ~ grphyp to a third element. $)
  ringhyp $p |- ( ( Base ` ndx ) =/= ( .r ` ndx )
               /\ ( +g ` ndx ) =/= ( .r ` ndx ) ) $=
    ( vf cnx cbs cfv cmulr wne cplusg c3 c1 basendx 1re 3re 1lt3 ltneii
    eqnetri df-mulr 3nn ndxargi neeqtrri c2 df-plusg 2nn 2re 2lt3 pm3.2i ) BCDZ
    BEDZFBGDZUGFUFHUGUFIHJIHKLMNOEAHAPQRZSUHHUGUHTHGATAUAUBRTHUCLUDNOUISUE $.
    $( [8-Jun-2013] $)

  ${
    $d r B $.  $d r P $.  $d r T $.
    ringbase.b $e |- B e. _V $.
    ringbase.p $e |- P e. _V $.
    ringbase.t $e |- T e. _V $.
    ringbase.r $e |- R
               = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. ,
                   <. ( .r ` ndx ) , T >. } $.
    $( The base set of a constructed ring. $)
    ringbase $p |- B = ( Base ` R ) $=
      ( vf cbs cplusg cmulr baseid plusgid mulrid grphyp ringhyp str3v1 ) JKLIC
      ABDIMINIOHEFGPQR $.
      $( [8-Jun-2013] $)

    $( The additive operation of a constructed ring. $)
    ringplusg $p |- P = ( +g ` R ) $=
      ( vf cbs cplusg cmulr baseid plusgid mulrid grphyp ringhyp str3v2 ) JKLIC
      ABDIMINIOHEFGPQR $.
      $( [8-Jun-2013] $)

    $( The muliplicative operation of a constructed ring. $)
    ringmulr $p |- T = ( .r ` R ) $=
      ( vf cbs cplusg cmulr baseid plusgid mulrid grphyp ringhyp str3v3 ) JKLIC
      ABDIMINIOHEFGPQR $.
      $( [8-Jun-2013] $)
  $}

  ${
    $d u r x B $.  $d u r x R $.  $d r T $.
    ringidval.b $e |- B = ( Base ` R ) $.
    ringidval.t $e |- T = ( .r ` R ) $.
    ringidval.u $e |- U = ( 1r ` R ) $.
    $( The value of the unity element of a ring. $)
    ringidval $p |- ( R e. V -> U = ( iota_ u e. B A. x e. B
               ( ( u T x ) = x /\ ( x T u ) = x ) ) ) $=
      ( vr wcel cvv cv co wceq wa wral crio elisset cur cfv cmulr cbs fveq2
      syl6eqr opreqd eqeq1d anbi12d raleqbidv riotaeqbidv df-ur riotaex fvmpt
      syl5eq syl ) DGLDMLZFBNZANZEOZUSPZUSUREOZUSPZQZACRZBCSZPDGTUQFDUAUBVFJKDU
      RUSKNZUCUBZOZUSPZUSURVHOZUSPZQZAVGUDUBZRZBVNSVFMUAVGDPZVOVEBVNCVPVNDUDUBC
      VGDUDUEHUFZVPVMVDAVNCVQVPVJVAVLVCVPVIUTUSVPVHEURUSVPVHDUCUBEVGDUCUEIUFZUG
      UHVPVKVBUSVPVHEUSURVRUGUHUIUJUKABKULVEBCUMUNUOUP $.
      $( [7-Aug-2013] $) $( [27-Aug-2011] $)
  $}

  ${
    $d b r p t u x y z B $.  $d b r p t u x y z P $.  $d b r p t R $.
    $d b p r t u x y z T $.
    isring.b $e |- B = ( Base ` R ) $.
    isring.p $e |- P = ( +g ` R ) $.
    isring.t $e |- T = ( .r ` R ) $.
    $( The predicate "is a (unital) ring."  Definition of ring with unit in
       [Schechter] p. 187. $)
    isring $p |- ( R e. Ring <-> ( R e. Abel
             /\ A. x e. B A. y e. B A. z e. B ( ( x T y ) e. B
               /\ ( ( ( x T y ) T z ) = ( x T ( y T z ) )
            /\ ( x T ( y P z ) ) = ( ( x T y ) P ( x T z ) )
            /\ ( ( x P y ) T z ) = ( ( x T z ) P ( y T z ) ) ) )
           /\ E. u e. B A. x e. B ( ( u T x ) = x /\ ( x T u ) = x ) ) ) $=
      ( vb vr vp vt crg wcel cv cbs cfv wceq cplusg cmulr w3a co wa wral wrex
      wex cabel crab df-ring eleq2i fveq2 syl6eqr eqeq2d 3anbi123d 3anbi1d
      3anass syl6bb 3exbidv elrab cvv fvex eqeltri eleq2 anbi1d raleqbi1dv
      raleq rexeqbi1dv anbi12d opreq opreq2d eqeq12d opreq1d 3anbi23d anbi2d
      ralbidv 2ralbidv eleq1d eqtrd opreq12d eqeq1d rexralbidv ceqsex3v anbi2i
      bitr4i 3bitri ) GPQGLRZMRZSTZUAZNRZWJUBTZUAZORZWJUCTZUAZUDZARZBRZWPUEZWIQ
      ZXBCRZWPUEZWTXAXDWPUEZWPUEZUAZWTXAXDWMUEZWPUEZXBWTXDWPUEZWMUEZUAZWTXAWMUE
      ZXDWPUEZXKXFWMUEZUAZUDZUFZCWIUGZBWIUGZAWIUGZDRZWTWPUEZWTUAZWTYCWPUEZWTUAZ
      UFZAWIUGZDWIUHZUDZOUINUILUIZMUJUKZQGUJQZWIEUAZWMFUAZWPHUAZUDZYBYJUFZUFZOU
      INUILUIZUFZYNWTXAHUEZEQZUUCXDHUEZWTXAXDHUEZHUEZUAZWTXAXDFUEZHUEZUUCWTXDHU
      EZFUEZUAZWTXAFUEZXDHUEZUUKUUFFUEZUAZUDZUFZCEUGZBEUGAEUGZYCWTHUEZWTUAZWTYC
      HUEZWTUAZUFZAEUGDEUHZUDZPYMGABCDOMLNULUMYLUUAMGUJWJGUAZYKYTLNOUVIYKYRYBYJ
      UDYTUVIWSYRYBYJUVIWLYOWOYPWRYQUVIWKEWIUVIWKGSTZEWJGSUNIUOUPUVIWNFWMUVIWNG
      UBTZFWJGUBUNJUOUPUVIWQHWPUVIWQGUCTZHWJGUCUNKUOUPUQURYRYBYJUSUTVAVBUUBYNUV
      AUVGUFZUFUVHUUAUVMYNYSXBEQZXRUFZCEUGZBEUGZAEUGZYHAEUGZDEUHZUFUVNXHWTUUIWP
      UEZXBXKFUEZUAZUUNXDWPUEZXKXFFUEZUAZUDZUFZCEUGZBEUGAEUGZUVTUFUVMLNOEFHEUVJ
      VCIGSVDVEFUVKVCJGUBVDVEHUVLVCKGUCVDVEYOYBUVRYJUVTYAUVQAWIEXTUVPBWIEXSUVOC
      WIEYOXCUVNXRWIEXBVFVGVHVHVHYIUVSDWIEYHAWIEVIVJVKYPUVRUWJUVTYPUVPUWIABEEYP
      UVOUWHCEYPXRUWGUVNYPXMUWCXQUWFXHYPXJUWAXLUWBYPXIUUIWTWPXAXDWMFVLVMXBXKWMF
      VLVNYPXOUWDXPUWEYPXNUUNXDWPWTXAWMFVLVOXKXFWMFVLVNVPVQVRVSVGYQUWJUVAUVTUVG
      YQUWIUUTABEEYQUWHUUSCEYQUVNUUDUWGUURYQXBUUCEWTXAWPHVLZVTYQXHUUHUWCUUMUWFU
      UQYQXEUUEXGUUGYQXEXBXDHUEUUEXBXDWPHVLYQXBUUCXDHUWKVOWAYQXGWTXFHUEUUGWTXFW
      PHVLYQXFUUFWTHXAXDWPHVLZVMWAVNYQUWAUUJUWBUULWTUUIWPHVLYQXBUUCXKUUKFUWKWTX
      DWPHVLZWBVNYQUWDUUOUWEUUPUUNXDWPHVLYQXKUUKXFUUFFUWMUWLWBVNUQVKVRVSYQYHUVF
      DAEEYQYEUVCYGUVEYQYDUVBWTYCWTWPHVLWCYQYFUVDWTWTYCWPHVLWCVKWDVKWEWFYNUVAUV
      GUSWGWH $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d b p r t u x y z B $.  $d b p r t u x y z P $.  $d b n p r t R $.
    $d b p r t u x y z T $.  $d n u x y z $.
    ringi.b $e |- B = ( Base ` R ) $.
    ringi.p $e |- P = ( +g ` R ) $.
    ringi.t $e |- T = ( .r ` R ) $.
    $d x y z X $.  $d y z Y $.  $d z Z $.
    $( Properties of a unital ring. $)
    ringi $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) )
           -> ( ( X T Y ) e. B
                /\ ( ( ( X T Y ) T Z ) = ( X T ( Y T Z ) )
              /\ ( X T ( Y P Z ) ) = ( ( X T Y ) P ( X T Z ) )
                /\ ( ( X P Y ) T Z ) = ( ( X T Z ) P ( Y T Z ) ) ) ) ) $=
      ( vx vy vz vu crg wcel cv co wceq w3a wa wral cabel wrex isring simp2bi
      opreq1 eleq1d opreq1d eqeq12d opreq12d 3anbi123d anbi12d opreq2 opreq2d
      anbi2d rcla43v mpan9 ) COPZKQZLQZDRZAPZVBMQZDRZUTVAVDDRZDRZSZUTVAVDBRZDRZ
      VBUTVDDRZBRZSZUTVABRZVDDRZVKVFBRZSZTZUAZMAUBLAUBKAUBZEAPFAPGAPTEFDRZAPZWA
      GDRZEFGDRZDRZSZEFGBRZDRZWAEGDRZBRZSZEFBRZGDRZWIWDBRZSZTZUAZUSCUCPVTNQZUTD
      RUTSUTWRDRUTSUAKAUBNAUDKLMNABCDHIJUEUFVSWQEVADRZAPZWSVDDRZEVFDRZSZEVIDRZW
      SEVDDRZBRZSZEVABRZVDDRZXEVFBRZSZTZUAWBWAVDDRZEFVDDRZDRZSZEFVDBRZDRZWAXEBR
      ZSZWLVDDRZXEXNBRZSZTZUAKLMEFGAAAUTESZVCWTVRXLYEVBWSAUTEVADUGZUHYEVHXCVMXG
      VQXKYEVEXAVGXBYEVBWSVDDYFUIUTEVFDUGUJYEVJXDVLXFUTEVIDUGYEVBWSVKXEBYFUTEVD
      DUGZUKUJYEVOXIVPXJYEVNXHVDDUTEVABUGUIYEVKXEVFBYGUIUJULUMVAFSZWTWBXLYDYHWS
      WAAVAFEDUNZUHYHXCXPXGXTXKYCYHXAXMXBXOYHWSWAVDDYIUIYHVFXNEDVAFVDDUGZUOUJYH
      XDXRXFXSYHVIXQEDVAFVDBUGUOYHWSWAXEBYIUIUJYHXIYAXJYBYHXHWLVDDVAFEBUNUIYHVF
      XNXEBYJUOUJULUMVDGSZYDWPWBYKXPWFXTWKYCWOYKXMWCXOWEVDGWADUNYKXNWDEDVDGFDUN
      ZUOUJYKXRWHXSWJYKXQWGEDVDGFBUNUOYKXEWIWABVDGEDUNZUOUJYKYAWMYBWNVDGWLDUNYK
      XEWIXNWDBYMYLUKUJULUPUQUR $.
      $( [26-Aug-2011] $)
  $}

  ${
    ringcl.b $e |- B = ( Base ` R ) $.
    ringcl.t $e |- T = ( .r ` R ) $.
    $( Closure of the multiplication operation of a ring. $)
    ringcl $p |- ( ( R e. Ring /\ X e. B /\ Y e. B )
          -> ( X T Y ) e. B ) $=
      ( crg wcel co wa w3a 3anass anidm anbi2i bitri wceq cplusg cfv eqid
      ringi simpld sylan2br 3impb ) BHIZDAIZEAIZDECJZAIZUFUGKZUEUFUGUGLZUIUKUFU
      GUGKZKUJUFUGUGMULUGUFUGNOPUEUKKUIUHECJDEECJZCJQDEEBRSZJCJUHUHUNJQDEUNJECJ
      UHUMUNJQLAUNBCDEEFUNTGUAUBUCUD $.
      $( [26-Aug-2011] $)

    $( Associative law for the multiplication operation of a ring. $)
    ringass $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                      ( ( X T Y ) T Z ) = ( X T ( Y T Z ) ) ) $=
      ( crg wcel w3a wa co wceq cplusg cfv eqid ringi simpr1 syl ) BIJDAJEAJFAJ
      KLDECMZAJZUAFCMDEFCMZCMNZDEFBOPZMCMUADFCMZUEMNZDEUEMFCMUFUCUEMNZKLUDAUEBC
      DEFGUEQHRUBUDUGUHST $.
      $( [27-Aug-2011] $)

    $d r u w x y z B $.  $d r u x y z R $.  $d r u w x y z T $.
    $( The unit element of a ring is unique. $)
    ringideu $p |- ( R e. Ring ->
                 E! u e. B A. x e. B ( ( u T x ) = x /\ ( x T u ) = x ) ) $=
      ( vw vy vz crg wcel cv co wceq wa wral wrex wi wreu cabel cplusg cfv w3a
      eqid isring simp3bi simpl ralimi opreq2 id eqeq12d rcla4v syl5 simpr
      opreq1 im2anan9 eqtr2 ancoms syl6 rgen2a jctir eqeq1d anbi12d ralbidv
      reu4 sylibr ) DKLZBMZAMZENZVJOZVJVIENZVJOZPZACQZBCRZVPHMZVJENZVJOZVJVRENZ
      VJOZPZACQZPZVIVROZSZHCQBCQZPVPBCTVHVQWHVHDUALVJIMZENZCLWJJMZENVJWIWKENZEN
      OVJWIWKDUBUCZNENWJVJWKENZWMNOVJWIWMNWKENWNWLWMNOUDPJCQICQACQVQAIJBCWMDEFW
      MUEGUFUGWGBHCVRCLZVICLZWGWOWPPWEVIVRENZVROZWQVIOZPWFWOVPWRWPWDWSVPVLACQWO
      WRVOVLACVLVNUHUIVLWRAVRCVJVROZVKWQVJVRVJVRVIEUJWTUKULUMUNWDWBACQWPWSWCWBA
      CVTWBUOUIWBWSAVICVJVIOZWAWQVJVIVJVIVREUPXAUKULUMUNUQWSWRWFWQVIVRURUSUTUSV
      AVBVPWDBHCWFVOWCACWFVLVTVNWBWFVKVSVJVIVRVJEUPVCWFVMWAVJVIVRVJEUJVCVDVEVFV
      G $.
      $( [27-Aug-2011] $)
  $}

  ${
    ringdi.b $e |- B = ( Base ` R ) $.
    ringdi.p $e |- P = ( +g ` R ) $.
    ringdi.t $e |- T = ( .r ` R ) $.
    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringdi $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                     ( X T ( Y P Z ) ) = ( ( X T Y ) P ( X T Z ) ) ) $=
      ( crg wcel w3a wa co wceq ringi simpr2 syl ) CKLEALFALGALMNEFDOZALZTGDOEF
      GDOZDOPZEFGBODOTEGDOZBOPZEFBOGDOUDUBBOPZMNUEABCDEFGHIJQUAUCUEUFRS $.
      $( [27-Aug-2011] $)

    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    ringdir $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                      ( ( X P Y ) T Z ) = ( ( X T Z ) P ( Y T Z ) ) ) $=
      ( crg wcel w3a wa co wceq ringi simpr3 syl ) CKLEALFALGALMNEFDOZALZTGDOEF
      GDOZDOPZEFGBODOTEGDOZBOPZEFBOGDOUDUBBOPZMNUFABCDEFGHIJQUAUCUEUFRS $.
      $( [27-Aug-2011] $)
  $}

  ${
    $d u x B $.  $d u x R $.
    ringidcl.b $e |- B = ( Base ` R ) $.
    ringidcl.u $e |- U = ( 1r ` R ) $.
    $( The unit element of a ring belongs to the base set of the ring. $)
    ringidcl $p |- ( R e. Ring -> U e. B ) $=
      ( vu vx crg wcel cv cmulr cfv co wceq wa wral wreu eqid ringideu crio
      ringidval adantr riotacl adantl eqeltrd mpdan ) BHIZFJZGJZBKLZMUINUIUHUJM
      UINOGAPZFAQZCAIGFABUJDUJRZSUGULOCUKFATZAUGCUNNULGFABUJCHDUMEUAUBULUNAIUGU
      KFAUCUDUEUF $.
      $( [27-Aug-2011] $)
  $}

  ${
    $d r u x y z R $.
    $( A ring is an Abelian group. $)
    ringabl $p |- ( R e. Ring -> R e. Abel ) $=
      ( vx vy vz vu crg wcel cabel cv cmulr cfv co cbs wceq cplusg w3a wa wral
      wrex eqid isring biimpi simp1d ) AFGZAHGZBIZCIZAJKZLZAMKZGUIDIZUHLUFUGUKU
      HLZUHLNUFUGUKAOKZLUHLUIUFUKUHLZUMLNUFUGUMLUKUHLUNULUMLNPQDUJRCUJRBUJRZEIZ
      UFUHLUFNUFUPUHLUFNQBUJREUJSZUDUEUOUQPBCDEUJUMAUHUJTUMTUHTUAUBUC $.
      $( [26-Aug-2011] $)
  $}

  $( A ring is a group. $)
  ringgrp $p |- ( R e. Ring -> R e. Grp ) $=
    ( crg wcel cabel cgrp ringabl ablgrp syl ) ABCADCAECAFAGH $.
    $( [15-Sep-2011] $)

  ${
    $d u w x B $.  $d u w x I $.  $d u x R $.  $d u w x T $.  $d u w x U $.
    $d x X $.
    ringidm.b $e |- B = ( Base ` R ) $.
    ringidm.t $e |- T = ( .r ` R ) $.
    ringidm.u $e |- U = ( 1r ` R ) $.
    $( Lemma for ~ ringlidm and ~ ringridm . $)
    ringidmlem $p |- ( ( R e. Ring /\ X e. B )
           -> ( ( U T X ) = X /\ ( X T U ) = X ) ) $=
      ( vx vu vw crg wcel cv co wceq wa wral crio ringidval eqcomd wreu wb
      ringidcl ringideu ax-17 a17d opreq1 eqeq1d opreq2 anbi12d ralbidv
      riota2f syl11anc mpbird id eqeq12d rcla4v mpan9 ) BLMZDINZCOZVAPZVADCOZVA
      PZQZIARZEAMDECOZEPZEDCOZEPZQZUTVGJNZVACOZVAPZVAVMCOZVAPZQZIARZJASZDPZUTDV
      TIJABCDLFGHTUAUTDAMZVSJAUBVGWAUCABDFHUDIJABCFGUEVSVGJKADKNDMJUFWBVGJUGVMD
      PZVRVFIAWCVOVCVQVEWCVNVBVAVMDVACUHUIWCVPVDVAVMDVACUJUIUKULUMUNUOVFVLIEAVA
      EPZVCVIVEVKWDVBVHVAEVAEDCUJWDUPZUQWDVDVJVAEVAEDCUHWEUQUKURUS $.
      $( [15-Sep-2011] $)

    $( The unit element of a ring is a left multiplicative identity. $)
    ringlidm $p |- ( ( R e. Ring /\ X e. B ) -> ( U T X ) = X ) $=
      ( crg wcel wa co wceq ringidmlem simpld ) BIJEAJKDECLEMEDCLEMABCDEFGHNO
      $.
      $( [15-Sep-2011] $)

    $( The unit element of a ring is a right multiplicative identity. $)
    ringridm $p |- ( ( R e. Ring /\ X e. B ) -> ( X T U ) = X ) $=
      ( crg wcel wa co wceq ringidmlem simprd ) BIJEAJKDECLEMEDCLEMABCDEFGHNO
      $.
      $( [15-Sep-2011] $)

    $( Properties showing that an element ` I ` is the unity element of a
       ring. $)
    isringid $p |- ( R e. Ring
      -> ( ( I e. B /\ A. x e. B ( ( I T x ) = x /\ ( x T I ) = x ) )
        <-> U = I ) ) $=
      ( vu vw crg wcel cv co wceq wa wral crio ringidval adantr wreu wb
      ringideu anim2i ancoms ax-17 a17d opreq1 eqeq1d opreq2 anbi12d ralbidv
      riota2f syl biimpd impr eqtrd ex ringidcl ringidmlem r19.21aiva jca
      eleq1 syl5ibcom impbid ) CLMZFBMZFANZDOZVIPZVIFDOZVIPZQZABRZQZEFPZVGVPVQV
      GVPQEJNZVIDOZVIPZVIVRDOZVIPZQZABRZJBSZFVGEWEPVPAJBCDELGHITUAVGVHVOWEFPZVG
      VHQZVOWFWGVHWDJBUBZQZVOWFUCVHVGWIVGWHVHAJBCDGHUDUEUFWDVOJKBFKNFMJUGVHVOJU
      HVRFPZWCVNABWJVTVKWBVMWJVSVJVIVRFVIDUIUJWJWAVLVIVRFVIDUKUJULUMUNUOUPUQURU
      SVGEBMZEVIDOZVIPZVIEDOZVIPZQZABRZQVQVPVGWKWQBCEGIUTVGWPABBCDEVIGHIVAVBVCV
      QWKVHWQVOEFBVDVQWPVNABVQWMVKWOVMVQWLVJVIEFVIDUIUJVQWNVLVIEFVIDUKUJULUMULV
      EVF $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d u x y z B $.  $d u x y z R $.  $d u T $.  $d u x U $.  $d u x y z ph $.
    isringd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isringd.p $e |- ( ph -> P = ( +g ` R ) ) $.
    isringd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isringd.g $e |- ( ph -> R e. Abel ) $.
    isringd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x T y ) e. B ) $.
    isringd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x T y ) T z ) = ( x T ( y T z ) ) ) $.
    isringd.d $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( x T ( y P z ) ) = ( ( x T y ) P ( x T z ) ) ) $.
    isringd.e $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) T z ) = ( ( x T z ) P ( y T z ) ) ) $.
    isringd.u $e |- ( ph -> U e. B ) $.
    isringd.i $e |- ( ( ph /\ x e. B ) -> ( U T x ) = x ) $.
    isringd.h $e |- ( ( ph /\ x e. B ) -> ( x T U ) = x ) $.
    $( Properties that determine a ring. $)
    isringd $p |- ( ph -> R e. Ring ) $=
      ( vu cabel wcel cv cmulr cfv co cbs wceq cplusg w3a wa wral wrex crg wi
      3adant3r3 3jca jca 3exp2 imp3a r19.21adv r19.21aivv r19.21aiva opreq1
      eqeq1d opreq2 anbi12d ralbidv rcla4ev syl11anc opreqd eleq12d eqidd
      opreq123d eqeq12d 3anbi123d raleqbidv rexeqbidv 3anbi23d mpbid eqid
      isring sylibr ) AGUBUCZBUDZCUDZGUEUFZUGZGUHUFZUCZWIDUDZWHUGZWFWGWLWHUGZWH
      UGZUIZWFWGWLGUJUFZUGZWHUGZWIWFWLWHUGZWQUGZUIZWFWGWQUGZWLWHUGZWTWNWQUGZUIZ
      UKZULZDWJUMZCWJUMZBWJUMZUAUDZWFWHUGZWFUIZWFXLWHUGZWFUIZULZBWJUMZUAWJUNZUK
      ZGUOUCAWEWFWGHUGZEUCZYAWLHUGZWFWGWLHUGZHUGZUIZWFWGWLFUGZHUGZYAWFWLHUGZFUG
      ZUIZWFWGFUGZWLHUGZYIYDFUGZUIZUKZULZDEUMZCEUMZBEUMZXLWFHUGZWFUIZWFXLHUGZWF
      UIZULZBEUMZUAEUNZUKXTAWEYTUUGMAYRBCEEAWFEUCZWGEUCZULYQDEAUUHUUIWLEUCZYQUP
      AUUHUUIUUJYQAUUHUUIUUJUKULZYBYPAUUHUUIYBUUJNUQUUKYFYKYOOPQURUSUTVAVBVCAIE
      UCIWFHUGZWFUIZWFIHUGZWFUIZULZBEUMZUUGRAUUPBEAUUHULUUMUUOSTUSVDUUFUUQUAIEX
      LIUIZUUEUUPBEUURUUBUUMUUDUUOUURUUAUULWFXLIWFHVEVFUURUUCUUNWFXLIWFHVGVFVHV
      IVJVKURAYTXKUUGXSWEAYSXJBEWJJAYRXICEWJJAYQXHDEWJJAYBWKYPXGAYAWIEWJAHWHWFW
      GLVLZJVMAYFWPYKXBYOXFAYCWMYEWOAYAWIWLWLHWHLUUSAWLVNZVOAWFWFYDWNHWHLAWFVNZ
      AHWHWGWLLVLZVOVPAYHWSYJXAAWFWFYGWRHWHLUVAAFWQWGWLKVLVOAYAWIYIWTFWQKUUSAHW
      HWFWLLVLZVOVPAYMXDYNXEAYLXCWLWLHWHLAFWQWFWGKVLUUTVOAYIWTYDWNFWQKUVCUVBVOV
      PVQVHVRVRVRAUUFXRUAEWJJAUUEXQBEWJJAUUBXNUUDXPAUUAXMWFAHWHXLWFLVLVFAUUCXOW
      FAHWHWFXLLVLVFVHVRVSVTWABCDUAWJWQGWHWJWBWQWBWHWBWCWD $.
      $( [2-Aug-2013] $)
  $}

  ${
    ringz.b $e |- B = ( Base ` R ) $.
    ringz.t $e |- T = ( .r ` R ) $.
    ringz.z $e |- Z = ( 0g ` R ) $.
    $( The zero of a unital ring is a left absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    ringlz $p |- ( ( R e. Ring /\ X e. B ) -> ( Z T X ) = Z ) $=
      ( crg wcel wa co cplusg cfv wceq cgrp ringgrp grpidcl eqid grplid mpdan
      syl adantr opreq1d w3a simpr 3jca ringdir syldan simpl ringcl syl111anc
      grprid eqcomd syl11anc 3eqtr3d wb grplcan syl13anc mpbid ) BIJZDAJZKZEDCL
      ZVDBMNZLZVDEVELZOZVDEOZVCEEVELZDCLZVDVFVGVCVJEDCVAVJEOZVBVABPJZVLBQZVMEAJ
      ZVLABEFHRZAVEBEEFVESZHTUAUBUCUDVAVBVOVOVBUEVKVFOVCVOVOVBVAVOVBVAVMVOVNVPU
      BUCZVRVAVBUFZUGAVEBCEEDFVQGUHUIVCVMVDAJZVDVGOVAVMVBVNUCZVCVAVOVBVTVAVBUJV
      RVSABCEDFGUKULZVMVTKVGVDAVEBEVDFVQHUMUNUOUPVCVMVTVOVTVHVIUQWAWBVRWBAVEBVD
      EVDFVQURUSUT $.
      $( [15-Sep-2011] $)

    $( The zero of a unital ring is a right absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    ringrz $p |- ( ( R e. Ring /\ X e. B ) -> ( X T Z ) = Z ) $=
      ( crg wcel wa co cplusg cfv wceq cgrp ringgrp grpidcl eqid grplid mpdan
      syl adantr opreq2d w3a simpr 3jca ringdi syldan ringcl mpd3an3 eqcomd
      syl11anc 3eqtr3d wb grprcan syl13anc mpbid ) BIJZDAJZKZDECLZVBBMNZLZEVBVC
      LZOZVBEOZVADEEVCLZCLZVBVDVEVAVHEDCUSVHEOZUTUSBPJZVJBQZVKEAJZVJABEFHRZAVCB
      EEFVCSZHTUAUBUCUDUSUTUTVMVMUEVIVDOVAUTVMVMUSUTUFUSVMUTUSVKVMVLVNUBUCZVPUG
      AVCBCDEEFVOGUHUIVAVKVBAJZVBVEOUSVKUTVLUCZUSUTVMVQVPABCDEFGUJUKZVKVQKVEVBA
      VCBEVBFVOHTULUMUNVAVKVQVMVQVFVGUOVRVSVPVSAVCBVBEVBFVOUPUQUR $.
      $( [15-Sep-2011] $)
  $}

  $c DivRing $.

  $( Extend class notation with class of all division rings. $)
  cdivring $a class DivRing $.

  $( Define class of all division rings. $)
  df-drng $a |- DivRing = { r e. Ring |
    { <. 1 , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. , <. 2 , ( .r ` r ) >. }
      e. Grp } $.

  ${
    $d r G $.  $d r R $.
    isdivrng.b $e |- B = ( Base ` R ) $.
    isdivrng.t $e |- T = ( .r ` R ) $.
    isdivrng.z $e |- Z = ( 0g ` R ) $.
    isdivrng.g $e |- G = { <. 1 , ( B \ { Z } ) >. , <. 2 , T >. } $.
    $( The predicate "is a division ring". $)
    isdivrng $p |- ( R e. DivRing <-> ( R e. Ring /\ G e. Grp ) ) $=
      ( vr c1 cv cbs cfv c0g csn cdif cop c2 cmulr cpr cgrp wcel crg cdivring
      wceq fveq2 syl6eqr difeq1d sneqd sneqi difeq2d eqtrd opeq2d preq12d
      eleq1d eleq1i syl6bbr df-drng elrab2 ) KJLZMNZVAONZPZQZRZSVATNZRZUAZUBUCZ
      DUBUCZJBUDUEVABUFZVJKAEPZQZRZSCRZUAZUBUCVKVLVIVQUBVLVFVOVHVPVLVEVNKVLVEAV
      DQVNVLVBAVDVLVBBMNAVABMUGFUHUIVLVDVMAVLVDBONZPVMVLVCVRVABOUGUJEVRHUKUHULU
      MUNVLVGCSVLVGBTNCVABTUGGUHUNUOUPDVQUBIUQURJUSUT $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d x y z B $.  $d x y z T $.  $d x U $.  $d x y z Z $.  $d x y z ph $.
    isdivrngdOLD.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isdivrngdOLD.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isdivrngdOLD.z $e |- ( ph -> Z = ( 0g ` R ) ) $.
    isdivrngdOLD.u $e |- ( ph -> U = ( 1r ` R ) ) $.
    isdivrngdOLD.r $e |- ( ph -> R e. Ring ) $.
    isdivrngdOLD.n $e |- ( ( ph /\ ( x e. B /\ x =/= Z )
            /\ ( y e. B /\ y =/= Z ) ) -> ( x T y ) =/= Z ) $.
    isdivrngdOLD.o $e |- ( ph -> U =/= Z ) $.
    isdivrngdOLD.i $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I e. B ) $.
    isdivrngdOLD.j $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I =/= Z ) $.
    isdivrngdOLD.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( I T x ) = U )
        $.
    $( Properties that determine a division ring. ` I ` (reciprocal) is
       normally dependent on ` x ` i.e. read it as ` I ( x ) ` ." $)
    isdivrngdOLD $p |- ( ph -> R e. DivRing ) $=
      ( vf vz crg wcel c1 cbs cfv c0g csn cdif cop c2 cmulr cpr cgrp wa
      cdivring difeq1d sneqd difeq2d eqtrd opeq2d preq12d eleq1d anbi2d cnx
      cplusg basendx opeq1i df-plusg 2nn ndxargi preq12i cvv wceq fvex
      syl6eqel difexg syl eqid grpbaseg syl11anc grpplusgg prex a1i cv wne co
      eldifsn w3a ringcl syl3an1 3expib eleq2d anbi12d opreqd eleq12d 3imtr4d
      3impib 3adant2r 3adant3r sylanbrc syl3an3b syl3an2b eldifi 3anim123i wi
      ringass ex 3anbi123d eqidd opreq123d eqeq12d imp sylan2 cur ringidcl
      mpbird ringlidm eqeq1d adantrr sylan2b isgrpd syl5eqelr mpbi2and
      isdivrng sylibr ) AEUBUCZUDEUEUFZEUGUFZUHZUIZUJZUKEULUFZUJZUMZUNUCZUOZEUP
      UCAYGUDDIUHZUIZUJZUKFUJZUMZUNUCZYQAUUCYPYGAUUBYOUNAYTYLUUAYNAYSYKUDAYSYHY
      RUIYKADYHYRJUQAYRYJYHAIYILURUSUTVAAFYMUKKVAVBVCVDNAUUBVEUEUFZYSUJZVEVFUFZ
      FUJZUMZUNUUEYTUUGUUAUUDUDYSVGVHUUFUKFVFTUKTVIVJVKVHVLABCUAYSFUUHHGAYSVMUC
      ZFVMUCZYSUUHUEUFVNADVMUCUUIADYHVMJEUEVOVPDYRVMVQVRZAFYMVMKEULVOVPZYSFUUHV
      MVMUUHVSZVTWAAUUIUUJFUUHVFUFVNUUKUULYSFUUHVMVMUUMWBWAUUHVMUCAUUEUUGWCWDBW
      EZYSUCZAUUNDUCZUUNIWFZUOZCWEZYSUCZUUNUUSFWGZYSUCZUUNDIWHZUUTAUURUUSDUCZUU
      SIWFZUOZUVBUUSDIWHAUURUVFWIUVADUCZUVAIWFUVBAUURUVDUVGUVEAUUPUVDUVGUUQAUUP
      UVDUVGAUUNYHUCZUUSYHUCZUOUUNUUSYMWGZYHUCZUUPUVDUOUVGAUVHUVIUVKAYGUVHUVIUV
      KNYHEYMUUNUUSYHVSZYMVSZWJWKWLAUUPUVHUVDUVIADYHUUNJWMZADYHUUSJWMZWNAUVAUVJ
      DYHAFYMUUNUUSKWOZJWPWQWRWSWTOUVADIWHXAXBXCUUOUUTUAWEZYSUCZWIAUUPUVDUVQDUC
      ZWIZUVAUVQFWGZUUNUUSUVQFWGZFWGZVNZUUOUUPUUTUVDUVRUVSUUNDYRXDUUSDYRXDUVQDY
      RXDXEAUVTUWDAUVHUVIUVQYHUCZWIZUVJUVQYMWGZUUNUUSUVQYMWGZYMWGZVNZUVTUWDAYGU
      WFUWJXFNYGUWFUWJYHEYMUUNUUSUVQUVLUVMXGXHVRAUUPUVHUVDUVIUVSUWEUVNUVOADYHUV
      QJWMXIAUWAUWGUWCUWIAUVAUVJUVQUVQFYMKUVPAUVQXJXKAUUNUUNUWBUWHFYMKAUUNXJZAF
      YMUUSUVQKWOXKXLWQXMXNAGDUCZGIWFGYSUCAUWLEXOUFZYHUCZAYGUWNNYHEUWMUVLUWMVSZ
      XPVRAGUWMDYHMJWPXQPGDIWHXAUUOAUURGUUNFWGZUUNVNZUVCAUUPUWQUUQAUUPUWQAUVHUW
      MUUNYMWGZUUNVNZUUPUWQAYGUVHUWSXFNYGUVHUWSYHEYMUWMUUNUVLUVMUWOXRXHVRUVNAUW
      PUWRUUNAGUWMUUNUUNFYMKMUWKXKXSWQXMXTYAUUOAUURHYSUCZUVCAUURUOHDUCHIWFUWTQR
      HDIWHXAYAUUOAUURHUUNFWGGVNUVCSYAYBYCYDYHEYMYOYIUVLUVMYIVSYOVSYEYF $.
      $( [2-Aug-1013] $)
  $}

  ${
    $d x y z B $.  $d y z I $.  $d x y z T $.  $d x U $.  $d x y z Z $.
    $d x y z ph $.
    isdivrngd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isdivrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isdivrngd.z $e |- ( ph -> Z = ( 0g ` R ) ) $.
    isdivrngd.u $e |- ( ph -> U = ( 1r ` R ) ) $.
    isdivrngd.r $e |- ( ph -> R e. Ring ) $.
    isdivrngd.n $e |- ( ( ph /\ ( x e. B /\ x =/= Z )
            /\ ( y e. B /\ y =/= Z ) ) -> ( x T y ) =/= Z ) $.
    isdivrngd.o $e |- ( ph -> U =/= Z ) $.
    isdivrngd.i $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I e. B ) $.
    isdivrngd.j $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I =/= Z ) $.
    isdivrngd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( x T I ) = U ) $.
    $( Properties that determine a division ring. ` I ` (reciprocal) is
       normally dependent on ` x ` i.e. read it as ` I ( x ) ` ."  Note that we
       are using the right reciprocal of ~ isgrprd rather than the left
       reciprocal of ~ isgrpd . $)
    isdivrngd $p |- ( ph -> R e. DivRing ) $=
      ( vf vz crg wcel c1 cbs cfv c0g csn cdif cop c2 cmulr cpr cgrp wa
      cdivring difeq1d sneqd difeq2d eqtrd opeq2d preq12d eleq1d anbi2d cnx
      cplusg basendx opeq1i df-plusg 2nn ndxargi preq12i cvv wceq fvex
      syl6eqel difexg syl eqid grpbaseg syl11anc grpplusgg prex a1i cv wne co
      eldifsn w3a ringcl syl3an1 3expib eleq2d anbi12d opreqd eleq12d 3imtr4d
      3impib 3adant2r 3adant3r sylanbrc syl3an3b syl3an2b eldifi 3anim123i wi
      ringass ex 3anbi123d eqidd opreq123d eqeq12d imp sylan2 cur ringidcl
      mpbird ringridm eqeq1d adantrr sylan2b isgrprd syl5eqelr mpbi2and
      isdivrng sylibr ) AEUBUCZUDEUEUFZEUGUFZUHZUIZUJZUKEULUFZUJZUMZUNUCZUOZEUP
      UCAYGUDDIUHZUIZUJZUKFUJZUMZUNUCZYQAUUCYPYGAUUBYOUNAYTYLUUAYNAYSYKUDAYSYHY
      RUIYKADYHYRJUQAYRYJYHAIYILURUSUTVAAFYMUKKVAVBVCVDNAUUBVEUEUFZYSUJZVEVFUFZ
      FUJZUMZUNUUEYTUUGUUAUUDUDYSVGVHUUFUKFVFTUKTVIVJVKVHVLABCUAYSFUUHHGAYSVMUC
      ZFVMUCZYSUUHUEUFVNADVMUCUUIADYHVMJEUEVOVPDYRVMVQVRZAFYMVMKEULVOVPZYSFUUHV
      MVMUUHVSZVTWAAUUIUUJFUUHVFUFVNUUKUULYSFUUHVMVMUUMWBWAUUHVMUCAUUEUUGWCWDBW
      EZYSUCZAUUNDUCZUUNIWFZUOZCWEZYSUCZUUNUUSFWGZYSUCZUUNDIWHZUUTAUURUUSDUCZUU
      SIWFZUOZUVBUUSDIWHAUURUVFWIUVADUCZUVAIWFUVBAUURUVDUVGUVEAUUPUVDUVGUUQAUUP
      UVDUVGAUUNYHUCZUUSYHUCZUOUUNUUSYMWGZYHUCZUUPUVDUOUVGAUVHUVIUVKAYGUVHUVIUV
      KNYHEYMUUNUUSYHVSZYMVSZWJWKWLAUUPUVHUVDUVIADYHUUNJWMZADYHUUSJWMZWNAUVAUVJ
      DYHAFYMUUNUUSKWOZJWPWQWRWSWTOUVADIWHXAXBXCUUOUUTUAWEZYSUCZWIAUUPUVDUVQDUC
      ZWIZUVAUVQFWGZUUNUUSUVQFWGZFWGZVNZUUOUUPUUTUVDUVRUVSUUNDYRXDUUSDYRXDUVQDY
      RXDXEAUVTUWDAUVHUVIUVQYHUCZWIZUVJUVQYMWGZUUNUUSUVQYMWGZYMWGZVNZUVTUWDAYGU
      WFUWJXFNYGUWFUWJYHEYMUUNUUSUVQUVLUVMXGXHVRAUUPUVHUVDUVIUVSUWEUVNUVOADYHUV
      QJWMXIAUWAUWGUWCUWIAUVAUVJUVQUVQFYMKUVPAUVQXJXKAUUNUUNUWBUWHFYMKAUUNXJZAF
      YMUUSUVQKWOXKXLWQXMXNAGDUCZGIWFGYSUCAUWLEXOUFZYHUCZAYGUWNNYHEUWMUVLUWMVSZ
      XPVRAGUWMDYHMJWPXQPGDIWHXAUUOAUURUUNGFWGZUUNVNZUVCAUUPUWQUUQAUUPUWQAUVHUU
      NUWMYMWGZUUNVNZUUPUWQAYGUVHUWSXFNYGUVHUWSYHEYMUWMUUNUVLUVMUWOXRXHVRUVNAUW
      PUWRUUNAUUNUUNGUWMFYMKUWKMXKXSWQXMXTYAUUOAUURHYSUCZUVCAUURUOHDUCHIWFUWTQR
      HDIWHXAYAUUOAUURUUNHFWGGVNUVCSYAYBYCYDYHEYMYOYIUVLUVMYIVSYOVSYEYF $.
      $( [10-Aug-2013] $)
  $}

  $( A division ring is a ring. $)
  divrngring $p |- ( R e. DivRing -> R e. Ring ) $=
    ( cdivring wcel crg c1 cbs cfv c0g csn cdif cop c2 cmulr cpr cgrp eqid
    isdivrng simplbi ) ABCADCEAFGZAHGZIJKLAMGZKNZOCSAUAUBTSPUAPTPUBPQR $.
    $( [8-Sep-2011] $)

  $( A division ring is a group. $)
  divrnggrp $p |- ( R e. DivRing -> R e. Grp ) $=
    ( cdivring wcel crg cgrp divrngring ringgrp syl ) ABCADCAECAFAGH $.
    $( [8-Sep-2011] $)

  ${
    divrngmgrp.b $e |- B = ( Base ` R ) $.
    divrngmgrp.t $e |- T = ( .r ` R ) $.
    divrngmgrp.z $e |- Z = ( 0g ` R ) $.
    divrngmgrp.g $e |- G = { <. 1 , ( B \ { Z } ) >. , <. 2 , T >. } $.
    $( A division ring contains a multiplicative group. $)
    divrngmgrp $p |- ( R e. DivRing -> G e. Grp ) $=
      ( cdivring wcel crg cgrp isdivrng simprbi ) BJKBLKDMKABCDEFGHINO $.
      $( [8-Sep-2011] $)
  $}

  ${
    divrngmcl.b $e |- B = ( Base ` R ) $.
    divrngmcl.t $e |- T = ( .r ` R ) $.
    divrngmcl.z $e |- Z = ( 0g ` R ) $.
    $( The product of two nonzero elements of a division ring is nonzero. $)
    divrngmcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } )
                    /\ Y e. ( B \ { Z } ) ) -> ( X T Y ) e. ( B \ { Z } ) ) $=
      ( cdivring wcel c1 csn cdif cop c2 cpr cgrp co eqid divrngmgrp cbs cfv
      prex baseval wne wceq 1ne2 cr 1re elisseti 2re cvv fvex eqeltri difexg
      ax-mp cmulr fvpr1 eqtr2i cplusg plusgval fvpr2 grpcl syl3an1 ) BJKLAFMZNZ
      OZPCOZQZRKDVGKEVGKDECSVGKABCVJFGHIVJTUAVGCVJDEVJUBUCLVJUCZVGVJVHVIUDZUELP
      UFZVKVGUGUHLPVGCLUIUJUKZPUIULUKZAUMKVGUMKABUBUCUMGBUBUNUOAVFUMUPUQZCBURUC
      UMHBURUNUOZUSUQUTVJVAUCPVJUCZCVJVLVBVMVRCUGUHLPVGCVNVOVPVQVCUQUTVDVE $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngid.b $e |- B = ( Base ` R ) $.
    divrngid.t $e |- T = ( .r ` R ) $.
    divrngid.z $e |- Z = ( 0g ` R ) $.
    divrngid.u $e |- U = ( 1r ` R ) $.
    divrngid.g $e |- G = { <. 1 , ( B \ { Z } ) >. , <. 2 , T >. } $.
    $( Lemma for ~ divrngid . $)
    divrngidlem $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cdivring wcel c0g cfv wne csn cdif cgrp crg wa isdivrng simpr sylbi
      cvv cbs fvex eqeltri difexg ax-mp cmulr grpbasex eqid grpidcl syl
      eldifsn wceq co divrngring divrngmgrp eldifi 3syl ringlidm syl11anc
      opreq1 sylan9req ringlz adantr eqtrd ex necon3d mpd ) BLMZENOZFPZDFPVMVNA
      FQZRZMZVOVMESMZVRVMBTMZVSUAVSABCEFGHIKUBVTVSUCUDVQEVNVQCEAUEMVQUEMABUFOUE
      GBUFUGUHAVPUEUIUJCBUKOUEHBUKUGUHKULVNUMUNZUOVRVNAMZVOUAVOVNAFUPWBVOUCUDUO
      VMDFVNFVMDFUQZVNFUQVMWCUAVNFVNCURZFVMWCVNDVNCURZWDVMVTWBWEVNUQBUSZVMVSVRW
      BABCEFGHIKUTWAVNAVPVAVBZABCDVNGHJVCVDDFVNCVEVFVMWDFUQZWCVMVTWBWHWFWGABCVN
      FGHIVGVDVHVIVJVKVL $.
      $( [8-Sep-2011] $)

    $( A division ring's unit is the identity element of its multiplicative
       group. $)
    divrngid $p |- ( R e. DivRing -> U = ( 0g ` G ) ) $=
      ( cdivring wcel c0g cfv co wceq crg divrngring csn cdif cgrp divrngmgrp
      cvv cbs fvex eqeltri difexg ax-mp cmulr grpbasex eqid grpidcl syl eldifi
      ringlidm syl11anc grpplusgx grplid eqtr4d wb wn ringidcl wne divrngidlem
      df-ne sylib elsni nsyl eldif sylanbrc grprcan syl13anc mpbid ) BLMZDENOZC
      PZVPVPCPZQZDVPQZVOVQVPVRVOBRMZVPAMZVQVPQBSZVOVPAFTZUAZMZWBVOEUBMZWFABCEFG
      HIKUCZWEEVPWECEAUDMWEUDMABUEOUDGBUEUFUGAWDUDUHUIZCBUJOUDHBUJUFUGZKUKZVPUL
      ZUMUNZVPAWDUOUNABCDVPGHJUPUQVOWGWFVRVPQWHWMWECEVPVPWKWECEWIWJKURZWLUSUQUT
      VOWGDWEMZWFWFVSVTVAWHVODAMZDWDMZVBWOVOWAWPWCABDGJVCUNVODFQZWQVODFVDWRVBAB
      CDEFGHIJKVEDFVFVGDFVHVIDAWDVJVKWMWMWECEDVPVPWKWNVLVMVN $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngunz.z $e |- Z = ( 0g ` R ) $.
    divrngunz.u $e |- U = ( 1r ` R ) $.
    $( A division ring's unit is different from its zero. $)
    divrngunz $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cbs cfv cmulr c1 csn cdif cop c2 cpr eqid divrngidlem ) AFGZAAHGZBIQCJK
      LMRLNZCQORODESOP $.
      $( [8-Sep-2011] $)
  $}


  $c invr $.

  $( Extend class notation with multiplicative inverse. $)
  cinvr $a class invr $.

  $( Define multiplicative inverse. $)
  df-invr $a |- invr = ( r e. _V |-> ( -g `
  { <. 1 , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. , <. 2 , ( .r ` r ) >. } ) ) $.

  ${
    $d r N $.  $d r R $.
    invrfval.b $e |- B = ( Base ` R ) $.
    invrfval.z $e |- Z = ( 0g ` R ) $.
    invrfval.t $e |- T = ( .r ` R ) $.
    invrfval.4 $e |- G =
           { <. 1 , ( B \ { Z } ) >. , <. 2 , T >. } $.
    invrfval.n $e |- N = ( -g ` G ) $.
    invrfval.i $e |- I = ( invr ` R ) $.
    $( Multiplicative inverse function for a division ring. $)
    invrfval $p |- ( R e. A -> I = N ) $=
      ( vr wcel cinvr cfv cvv wceq elisset c1 cv cbs c0g csn cdif cop c2 cmulr
      cpr cminusg fveq2 syl6eqr difeq1d sneqd difeq2d eqtrd opeq2d preq12d
      fveq2d df-invr fvex eqeltri fvmpt syl syl5eq ) CAPZFCQRZGNVHCSPVIGTCAUAOC
      UBOUCZUDRZVJUERZUFZUGZUHZUIVJUJRZUHZUKZULRZGSQVJCTZVSEULRZGVTVREULVTVRUBB
      HUFZUGZUHZUIDUHZUKEVTVOWDVQWEVTVNWCUBVTVNBVMUGWCVTVKBVMVTVKCUDRBVJCUDUMIU
      NUOVTVMWBBVTVLHVTVLCUERHVJCUEUMJUNUPUQURUSVTVPDUIVTVPCUJRDVJCUJUMKUNUSUTL
      UNVAMUNOVBGWASMEULVCVDVEVFVG $.
      $( [21-Sep-2011] $)
  $}

  ${
    invrcl.b $e |- B = ( Base ` R ) $.
    invrcl.z $e |- Z = ( 0g ` R ) $.
    invrcl.i $e |- I = ( invr ` R ) $.
    $( Closure of the multiplicative inverse in a division ring. $)
    invrcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } ) )
             -> ( I ` X ) e. ( B \ { Z } ) ) $=
      ( cdivring wcel csn cdif wa cfv c1 cop c2 cmulr cpr cminusg wceq eqid
      invrfval fveq1d adantr cgrp divrngmgrp cplusg cbs prex baseval wne 1ne2
      cr 1re elisseti 2re cvv fvex eqeltri difexg ax-mp fvpr1 eqtr2i grpinvcl
      sylan eqeltrd ) BIJZDAEKZLZJZMDCNZDOVJPZQBRNZPZSZTNZNZVJVHVLVRUAVKVHDCVQI
      ABVNVPCVQEFGVNUBZVPUBZVQUBZHUCUDUEVHVPUFJVKVRVJJABVNVPEFVSGVTUGVJVPUHNZVP
      VQDVPUINOVPNZVJVPVMVOUJUKOQULWCVJUAUMOQVJVNOUNUOUPQUNUQUPAURJVJURJABUINUR
      FBUIUSUTAVIURVAVBBRUSVCVBVDWBUBWAVEVFVG $.
      $( [21-Sep-2011] $)
  $}


