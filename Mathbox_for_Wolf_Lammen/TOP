$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                Mathbox for Wolf Lammen
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  $(
     Many, if not most, of the theorems in the section "Logical implication"
     are about handling chains of implications: ` ph -> ( ps -> ( ch -> ... `.
     With respect to chains, an exhaustive set of rules clarify

     - how to swap antecedents (com12, ...);

     - how to drop antecedents (ax-mp, pm2.43, ...);

     - how to add antecedents (a1i, ...)

     - how to replace an antecedent (syl, ...);

     - how to replace a consequent (ax-mp, syl, ...);

     - what is, when an antecedent equals the consequent (ax-1, id, ...).

     In all these cases, the operands of the chain have no inner structure, or
     it is of no importance.  I will call such a chain "simple" here.

     There is less support, when the operands are structured themselves.  Some
     kinds of inner structure involving the ` .- ` operator are best handled by
     the symmetric operators ` /\ ` and ` \/ `.  But a nested, simple chain
     has no such convenient replacement.  I can focus on the antecedents here,
     since a consequent representing a chain is in conjunction with its
     antecedents just an extended simple chain again.

     The following theorems show, how operations on nested chains appear
     somehow twisted: The minor premises of the syllogisms look reverted, in
     comparison to their normal counterparts, and while adding an antecedent to
     a chain via ~ a1i is easy, in nested chains they can be easily dropped.
  $)

  ${
    wl-jarri.1 $e |- ( ( ph -> ps ) -> ch ) $.
    $( Dropping a nested antecedent.  This theorem is one of two reversions of
       ~ ja .  Since ~ ja is reversible, one can conclude, that a nested (chain
       of) implication(s) is just a packed notation of two or more theorems/
       hypotheses with a common consequent.  (Contributed by Wolf Lammen,
       20-Sep-2013.)

       In ~ ax46 , this idea has been applied. $)
    wl-jarri $p |- ( ps -> ch ) $=
      ( wi ax-1 syl ) BABECBAFDG $.
      $( [20-Sep-2013] $)
  $}

  ${
    wl-mps.1 $e |- ( ph -> ( ps -> ch ) ) $.
    wl-mps.2 $e |- ( ( ph -> ch ) -> th ) $.
    $( Replacing a nested consequent.  A sort of modus ponens in antecedent
       position.  (Contributed by Wolf Lammen, 20-Sep-2013.) $)
    wl-mps $p |- ( ( ph -> ps ) -> th ) $=
      ( wi a2i syl ) ABGACGDABCEHFI $.
      $( [20-Sep-2013] $)
  $}

  ${
    wl-syls1.1 $e |- ( ps -> ch ) $.
    wl-syls1.2 $e |- ( ( ph -> ch ) -> th ) $.
    $( Replacing a nested consequent.  A sort of syllogism in antecedent
       position.  (Contributed by Wolf Lammen, 20-Sep-2013.) $)
    wl-syls1 $p |- ( ( ph -> ps ) -> th ) $=
      ( wi a1i wl-mps ) ABCDBCGAEHFI $.
      $( [20-Sep-2013] $)
  $}

  ${
    wl-syls2.1 $e |- ( ph -> ps ) $.
    wl-syls2.2 $e |- ( ( ph -> ch ) -> th ) $.
    $( Replacing a nested antecedent.  A sort of syllogism in antecedent
       position.  (Contributed by Wolf Lammen, 20-Sep-2013.) $)
    wl-syls2 $p |- ( ( ps -> ch ) -> th ) $=
      ( wi imim1i syl ) BCGACGDABCEHFI $.
      $( [20-Sep-2013] $)
  $}

$( (End of Wolf Lammen's mathbox.) $)


