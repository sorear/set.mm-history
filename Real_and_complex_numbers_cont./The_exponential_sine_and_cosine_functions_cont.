$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The exponential, sine, and cosine functions (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d F p q r v w z $.  $d G p q r v w z $.  $d O v w x y z $.  $d A x y $.
    $d H z $.  $d J z $.  $d C p q r w $.
    sinco.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( _i x. x ) ) } $.
    sinco.2 $e |- G = { <. x , y >. | ( x e. CC /\ y = ( -u _i x. x ) ) } $.

    ${
      sincolem.3 $e |- J = { <. x , y >. | ( x e. CC /\ y = ( x / A ) ) } $.
      sincolem.4 $e |- H = { <. w , v >. | ( w e. CC /\ v =
                 ( ( ( exp o. F ) ` w ) O ( ( exp o. G ) ` w ) ) ) } $.

      ${
        sincolem.5 $e |- ( ( ( ( exp o. F ) ` z ) e. CC /\
                             ( ( exp o. G ) ` z ) e. CC ) ->
               ( ( ( exp o. F ) ` z ) O ( ( exp o. G ) ` z ) ) e. CC ) $.
        sincolem.6 $e |- A e. CC $.
        sincolem.7 $e |- A =/= 0 $.
        $( Lemma for ~ sinco and ~ cosco . $)
        sincolem $p |- ( ( J o. H ) Fn CC /\
                         ( z e. CC -> ( ( J o. H ) ` z ) =
          ( ( ( exp ` ( _i x. z ) ) O ( exp ` ( -u _i x. z ) ) ) / A ) ) ) $=
          ( ccom cc wfn cv wcel cfv ci cmul co ce cneg cdiv wceq wi wf oprex
          fnopab2 wral fveq2 opreq12d eleq1d cbvralv eff axicn mulcl mpan
          fopab fco mp2an ffvelrni negcli sylanc mprgbir fopab2 mpbi fnfco
          wfun fnfun ax-mp fvco3 mp3an12 fvopab4g mpdan ffun opreq2 fveq2d
          eqtrd eqeltrrd cc0 wne divcl mp3an23 opreq1 syl 3eqtrd pm3.2i ) JISZT
          UAZCUBZTUCZWQWOUDZUEWQUFUGZUHUDZUEUIZWQUFUGZUHUDZKUGZFUJUGZUKULJTUAZT
          TIUMZWPABTAUBZFUJUGZJXIFUJUNNUOZDUBZUHGSZUDZXLUHHSZUDZKUGZTUCZDTUPZXH
          XSWQXMUDZWQXOUDZKUGZTUCZCTXRYCDCTXLWQUKZXQYBTYDXNXTXPYAKXLWQXMUQXLWQX
          OUQURZUSUTXTTUCYATUCYCWRPTTWQXMTTUHUMZTTGUMZTTXMUMVAABTTUEXIUFUGZGLUE
          TUCZXITUCZYHTUCVBUEXIVCVDVEZTTTUHGVFVGVHTTWQXOYFTTHUMZTTXOUMVAABTTXBX
          IUFUGZHMXBTUCZYJYMTUCUEVBVIZXBXIVCVDVEZTTTUHHVFVGVHVJZVKDETTXQIOVLVMZ
          TTJIVNVGWRWSWQIUDZJUDZXEJUDZXFJVOZXHWRWSYTUKXGUUBXKTJVPVQYRTTWQJIVRVS
          WRYSXEJWRYSYBXEWRYCYSYBUKYQDEWQXQYBTTIYEOVTWAWRXTXAYAXDKWRXTWQGUDZUHU
          DZXAUHVOZYGWRXTUUDUKYFUUEVATTUHWBVQZYKTTWQUHGVRVSWRUUCWTUHWRWTTUCZUUC
          WTUKYIWRUUGVBUEWQVCVDABWQYHWTTTGXIWQUEUFWCLVTWAWDWEWRYAWQHUDZUHUDZXDU
          UEYLWRYAUUIUKUUFYPTTWQUHHVRVSWRUUHXCUHWRXCTUCZUUHXCUKYNWRUUJYOXBWQVCV
          DABWQYMXCTTHXIWQXBUFWCMVTWAWDWEURZWEWDWRXETUCZUUAXFUKZWRYBXETUUKYQWFU
          ULXFTUCZUUMUULFTUCFWGWHUUNQRXEFWIWJABXEXJXFTTJXIXEFUJWKNVTWAWLWMWN $.
          $( [28-Nov-2007] $)
      $}

      ${
        sincnlem.5 $e |- A e. CC $.
        sincnlem.6 $e |- A =/= 0 $.
        sincnlem.7 $e |- C = ( abs o. - ) $.
        sincnlem.8 $e |- D = { <. <. p , q >. , r >. |
                               ( ( p e. ( CC X. CC ) /\ q e. ( CC X. CC ) ) /\
          r = sup ( { ( ( 1st ` p ) C ( 1st ` q ) ) ,
                      ( ( 2nd ` p ) C ( 2nd ` q ) ) } , RR , < ) ) } $.
        sincnlem.9 $e |- O e. ( ( Open ` D ) Cn ( Open ` C ) ) $.
        $( Lemma for ~ sincn and ~ coscn . $)
        sincnlem $p |- ( J o. H ) e. ( CC -cn-> CC ) $=
          ( ccom copn cfv ccn co cc ccncf wcel ce ci axicn mulc1cncf ax-mp
          eqid cncfmet1 eleqtri efcn cme w3a wa cnmet 3pm3.2i metcnco mpan
          mp2an cneg negcli cnmetba oprcn cc0 wne divccncf eleqtrri ) KJUEZFUFU
          GZVSUHUIZUJUJUKUIZJVTULZKVTULZVRVTULZUMHUEZVTULZUMIUEZVTULZWBHVTULZUM
          VTULZWFHWAVTUNUJULHWAULUOABUNHPUPUQFVSUBVSURZUSZUTUMWAVTVAWLUTZFVBULZ
          WNWNVCZWIWJVDWFWNWNWNFUBVEZWPWPVFZFFFHUMVSVSVSWKWKWKVGVHVIIVTULZWJWHI
          WAVTUNVJZUJULIWAULUNUOVKABWSIQUPUQWLUTWMWOWRWJVDWHWQFFFIUMVSVSVSWKWKW
          KVGVHVIONMCDFFFGVSWEWGJFVSVSVSGUFUGZLUJUJUJFUBVLZXAXAWPWPWPWPWKWKWKWT
          URWKUCUDSVMVIKWAVTEUJULEVNVOKWAULTUAABEKRVPVIWLUTWOWBWCVDWDWQFFFJKVSV
          SVSWKWKWKVGVHVIWLVQ $.
          $( [28-Nov-2007] $)
      $}
    $}

    ${
      sinco.3 $e |- J = { <. x , y >. |
                          ( x e. CC /\ y = ( x / ( 2 x. _i ) ) ) } $.
      sinco.4 $e |- H = { <. w , v >. | ( w e. CC /\
                     v = ( ( ( exp o. F ) ` w ) - ( ( exp o. G ) ` w ) ) ) } $.
      $( Sine expressed as a function composition.  (Contributed by Paul
         Chapman, 28-Nov-2007.) $)
      sinco $p |- sin = ( J o. H ) $=
        ( vz csin ccom wceq cv cfv cc wral wcel ci cmul co ce cneg cmin c2
        cdiv sinval wfn wi subcl 2cn axicn mulcli 2ne0 ine0 mulne0i sincolem
        pm3.27i eqtr4d rgen wb wf sinf ffn ax-mp pm3.26i eqfnfv2 mp2an mpbir )
        NHGOZPZMQZNRZVOVMRZPZMSTZVRMSVOSUAZVPUBVOUCUDUERUBUFVOUCUDUERUGUDUHUBUC
        UDZUIUDZVQVOUJVMSUKZVTVQWBPULZABMCDWAEFGHUGIJKLVOUEEORVOUEFORUMUHUBUNUO
        UPUHUBUNUOUQURUSUTZVAVBVCNSUKZWCVNVSVDSSNVEWFVFSSNVGVHWCWDWEVIMSNVMVJVK
        VL $.
        $( [28-Nov-2007] $)
    $}

    ${
      cosco.3 $e |- J = { <. x , y >. |
                          ( x e. CC /\ y = ( x / 2 ) ) } $.
      cosco.4 $e |- H = { <. w , v >. | ( w e. CC /\
                     v = ( ( ( exp o. F ) ` w ) + ( ( exp o. G ) ` w ) ) ) } $.
      $( Cosine expressed as a function composition.  (Contributed by Paul
         Chapman, 28-Nov-2007.) $)
      cosco $p |- cos = ( J o. H ) $=
        ( vz ccos ccom wceq cv cfv cc wral wcel ci cmul co ce cneg caddc c2
        cdiv cosval wfn wi addcl 2cn 2ne0 sincolem pm3.27i eqtr4d rgen wb wf
        cosf ffn ax-mp pm3.26i eqfnfv2 mp2an mpbir ) NHGOZPZMQZNRZVKVIRZPZMSTZV
        NMSVKSUAZVLUBVKUCUDUERUBUFVKUCUDUERUGUDUHUIUDZVMVKUJVISUKZVPVMVQPULZABM
        CDUHEFGHUGIJKLVKUEEORVKUEFORUMUNUOUPZUQURUSNSUKZVRVJVOUTSSNVAWAVBSSNVCV
        DVRVSVTVEMSNVIVFVGVH $.
        $( [28-Nov-2007] $)
    $}
  $}

  ${
    $d p q r v w x y $.
    $( Sine is continuous.  (Contributed by Paul Chapman, 28-Nov-2007.) $)
    sincn $p |- sin e. ( CC -cn-> CC ) $=
      ( vx vy vw vv vp vq vr csin cv cc wcel c2 ci cmul co cdiv wceq wa copab
      ce ccom cfv cneg cmin ccncf eqid sinco cabs cxp c1st c2nd cpr cr clt
      csup copab2 2cn axicn mulcli 2ne0 ine0 mulne0i copn subcn sincnlem
      eqeltri ) HAIZJKZBIZVGLMNOZPOQRABSZCIZJKDIVLTVHVIMVGNOQRABSZUAUBVLTVHVIMU
      CVGNOQRABSZUAUBUDOQRCDSZUAJJUEOABCDVMVNVOVKVMUFZVNUFZVKUFZVOUFZUGABCDVJUH
      UDUAZEIZJJUIZKFIZWBKRGIWAUJUBWCUJUBVTOWAUKUBWCUKUBVTOULUMUNUOQREFGUPZVMVN
      VOVKUDGFEVPVQVRVSLMUQURUSLMUQURUTVAVBVTUFZWDUFZEFGVTWDVTVCUBZWDVCUBZWEWFW
      GUFWHUFVDVEVF $.
      $( [28-Nov-2007] $)

    $( Cosine is continuous.  (Contributed by Paul Chapman, 28-Nov-2007.) $)
    coscn $p |- cos e. ( CC -cn-> CC ) $=
      ( vx vy vw vv vp vq vr ccos cv cc wcel c2 cdiv co wceq wa copab ce ci
      cmul ccom cfv cneg caddc ccncf eqid cosco cabs cmin cxp c1st c2nd cpr cr
      clt csup copab2 2cn 2ne0 copn addcn sincnlem eqeltri ) HAIZJKZBIZVDLMNOPA
      BQZCIZJKDIVHRVEVFSVDTNOPABQZUAUBVHRVEVFSUCVDTNOPABQZUAUBUDNOPCDQZUAJJUENA
      BCDVIVJVKVGVIUFZVJUFZVGUFZVKUFZUGABCDLUHUIUAZEIZJJUJZKFIZVRKPGIVQUKUBVSUK
      UBVPNVQULUBVSULUBVPNUMUNUOUPOPEFGUQZVIVJVKVGUDGFEVLVMVNVOURUSVPUFZVTUFZEF
      GVPVTVPUTUBZVTUTUBZWAWBWCUFWDUFVAVBVC $.
      $( [28-Nov-2007] $)
  $}

