$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Mapping of the exponential function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.  $d B x $.  $d C x $.
    efgh.1 $e |- F = ( x e. CC |-> ( exp ` ( A x. x ) ) ) $.
    $( The exponential function of a scaled complex number is a group
       homomorphism from the group of complex numbers under addition to the set
       of complex numbers under multiplication.  (Contributed by Paul Chapman,
       25-Apr-2008.)  (Revised by Mario Carneiro, 11-May-2014.) $)
    efgh $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( F ` ( B + C ) ) = ( ( F ` B ) x. ( F ` C ) ) ) $=
      ( cc wcel caddc co cmul cfv fveq2d wceq mulcl 3adant1 opreq2 fvex fvmpt
      ce w3a adddi 3adant3 3adant2 efadd syl2anc eqtrd addcl opreqan12d 3eqtr4d
      cv syl ) BGHZCGHZDGHZUAZBCDIJZKJZTLZBCKJZTLZBDKJZTLZKJZUQELZCELZDELZKJZUP
      USUTVBIJZTLZVDUPURVITBCDUBMUPUTGHZVBGHZVJVDNUMUNVKUOBCOUCUMUOVLUNBDOUDUTV
      BUEUFUGUPUQGHZVEUSNUNUOVMUMCDUHPAUQBAUKZKJZTLZUSGEVNUQNVOURTVNUQBKQMFURTR
      SULUNUOVHVDNUMUNUOVFVAVGVCKACVPVAGEVNCNVOUTTVNCBKQMFUTTRSADVPVCGEVNDNVOVB
      TVNDBKQMFVBTRSUIPUJ $.
      $( [11-May-2014] $) $( [25-Apr-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z ph $.  $d x y z X $.
    efghgrp.1 $e |- S = { y | E. x e. X y = ( exp ` ( A x. x ) ) } $.
    efghgrp.2 $e |- G = ( x. |` ( S X. S ) ) $.
    efghgrp.3 $e |- ( ph -> A e. CC ) $.
    efghgrp.4 $e |- ( ph -> X C_ CC ) $.
    efghgrp.5 $e |- ( + |` ( X X. X ) ) e. ( SubGrpOp ` + ) $.
    $( The image of a subgroup of the group ` + ` , under the exponential
       function of a scaled complex number, is an Abelian group.  (Contributed
       by Paul Chapman, 25-Apr-2008.)  (Revised by Mario Carneiro,
       12-May-2014.) $)
    efghgrp $p |- ( ph -> G e. AbelOp ) $=
      ( cmul cc cfv caddc wceq eqid wcel a1i vz cv co ce cmpt cxp cres crn cima
      cabl wrex cab rnmpt eqtr4i df-ima wss resmpt syl syl5eq cdm cgr ax-addopr
      rneqd fdmi csubg cnaddablo subgabl mp2an ablogrpo resgrprn syl3anc eqtr3d
      imaeq2d xpeq12d reseq2d ax-mp grporn wi wa mulcl efcl fmptd wfn ax-mulopr
      ex ssid wf ffn efgh 3expb sylan ghsubablo eqeltrd ) AFMBNDBUBZMUCZUDOZUEZ
      PGGUFUGZUHZUIZWTUFZUGZUJAFMEEUFZUGXBIAXCXAMAEWTEWTAEBGWPUEZUHZWTECUBZWPQB
      GUKCULXEHBCGWPXDXDRUMUNAWQGUIZXEWTAXGWQGUGZUHXEWQGUOAXHXDAGNUPZXHXDQKBNGW
      PUQURVCUSAGWSWQAPUTNNUFZQZWRVASZXIGWSQXKAXJNPVBVDZTAWRUJSZXLXNAPUJSZWRPVE
      OSZXNVFLPWRVGVHTZWRVIURKPWRNGWRRVJVKVMVLUSZXRVNVOUSACUANWRWQPXBMWTNNWSXPA
      LTPNXOPVASVFPVIVPXMVQABNWPNWQADNSZWNNSZWPNSZVRJXSXTYAXSXTVSWONSYADWNVTWOW
      AURWEURWQRZWBNNUPANWFTMXJWCZAXJNMWGYCWDXJNMWHVPTAXSXFNSZUAUBZNSZVSXFYEPUC
      WQOXFWQOYEWQOMUCQZJXSYDYFYGBDXFYEWQYBWIWJWKWSRWTRXBRXQWLWM $.
      $( [12-May-2014] $) $( [25-Apr-2008] $)
  $}

  ${
    $d y z $.  $d y A $.  $d y D $.
    efif1olem1.1 $e |- D = ( A (,] ( A + ( 2 x. pi ) ) ) $.
    $( Lemma for ~ efif1o . $)
    efif1olem1 $p |- ( ( A e. RR /\ ( x e. D /\ y e. D ) ) ->
                         ( abs ` ( x - y ) ) < ( 2 x. pi ) ) $=
      ( cr wcel cv wa cmin co c2 cpi clt wbr caddc wb readdcl mpbid syl3anc cfv
      cabs cmul cle cioc w3a simprl syl6eleq simpl 2re pire remulcli syl elioc2
      mpan2 syl2anc simp1d simprr absdiflt sylancl simp3d simp2d ltadd1 lelttrd
      a1i ltsubadd mpbird mpbir2and ) CFGZAHZDGZBHZDGZIZIZVJVLJKUBUALMUCKZNOZVL
      VPJKVJNOZVJVLVPPKZNOZVOVJFGZVLFGZVPFGZVQVRVTIQVOWACVJNOZVJCVPPKZUDOZVOVJC
      WEUEKZGZWAWDWFUFZVOVJDWGVIVKVMUGEUHVOVIWEFGZWHWIQVIVNUIZVOVIWJWKVIWCWJLMU
      JUKULZCVPRUOUMZCWEVJUNUPSZUQZVOWBCVLNOZVLWEUDOZVOVLWGGZWBWPWQUFZVOVLDWGVI
      VKVMUREUHVOVIWJWRWSQWKWMCWEVLUNUPSZUQZWCVOWLVEZVJVLVPUSTVOVRVLVJVPPKZNOZV
      OVLWEXCXAWMVOWAWCXCFGWOWLVJVPRUTVOWBWPWQWTVAVOWDWEXCNOZVOWAWDWFWNVBVOVIWA
      WCWDXEQWKWOXBCVJVPVCTSVDVOWBWCWAVRXDQXAXBWOVLVPVJVFTVGVOVJWEVSWOWMVOWBWCV
      SFGXAWLVLVPRUTVOWAWDWFWNVAVOWPWEVSNOZVOWBWPWQWTVBVOVIWBWCWPXFQWKXAXBCVLVP
      VCTSVDVH $.
      $( [13-May-2014] $)

    $( Lemma for ~ efif1o . $)
    efif1olem2 $p |- ( ( A e. RR /\ z e. RR ) ->
                       E. y e. D ( ( z - y ) / ( 2 x. pi ) ) e. ZZ ) $=
      ( cr wcel cmul co caddc cmin cdiv cz wbr syl syl2anc syl3anc wceq recnd
      cc cv wa c2 cpi cmo wrex cioc clt cle w3a simpl 2re pire remulcli readdcl
      wb mpan2 elioc2 crp resubcl pipos mulgt0ii elrpii modcl sylancl modlt a1i
      2pos ltadd2 mpbid ltaddsub cc0 modge0 subge02 mpbir3and syl6eleqr c1 cneg
      cfl cfv modval opreq2d wne gt0ne0ii redivcl mp3an23 remulcl sylancr recni
      flcl zre subsub simpr pnncan opreq1d 3eqtrd addcl subsub4 negsubdi2 pncan
      negeqd eqtr3d znegcl ax-mp zcn mulcomi mulm1i eqtri syl6eqr subdi 3eqtr2d
      1z eqtr4d zsubcl divcan3 eqtrd eqeltrd opreq2 eleq1d rcla4ev ) CFGZBUAZFG
      ZUBZCUCUDHIZJIZCYBKIZYEUEIZKIZDGYBYIKIZYELIZMGZYBAUAZKIZYELIZMGZADUFYDYIC
      YFUGIZDYDYIYQGZYIFGZCYIUHNZYIYFUINZYDYAYFFGZYRYSYTUUAUJUPYAYCUKZYDYAUUBUU
      CYAYEFGZUUBUCUDULUMUNZCYEUOUQOZCYFYIURPYDUUBYHFGZYSUUFYDYGFGZYEUSGZUUGCYB
      UTZYEUUEUCUDULUMVHVAVBZVCZYGYEVDVEZYFYHUTPYDCYHJIYFUHNZYTYDYHYEUHNZUUNYDU
      UHUUIUUOUUJUULYGYEVFVEYDUUGUUDYAUUOUUNUPUUMUUDYDUUEVGUUCYHYECVIQVJYDYAUUG
      UUBUUNYTUPUUCUUMUUFCYHYFVKQVJYDVLYHUINZUUAYDUUHUUIUUPUUJUULYGYEVMVEYDUUBU
      UGUUPUUAUPUUFUUMYFYHVNPVJVOEVPYDYKVQVRZYGYELIZVSVTZKIZMYDYKYEUUTHIZYELIZU
      UTYDYJUVAYELYDYJYBYEYBJIZYEUUSHIZJIZKIZYBUVCKIZUVDKIZUVAYDYIUVEYBKYDYIYFY
      GUVDKIZKIZYFYGKIZUVDJIZUVEYDYHUVIYFKYDUUHUUIYHUVIRUUJUULYGYEWAVEWBYDYFTGY
      GTGUVDTGZUVJUVLRYDYFUUFSYDYGUUJSYDUVDYDUUDUUSFGZUVDFGUUEYDUUSMGZUVNYDUURF
      GZUVOYDUUHUVPUUJUUHUUDYEVLWCZUVPUUEYEUUEUUKWDZYGYEWEWFOUURWJOZUUSWKOZYEUU
      SWGWHSZYFYGUVDWLQYDUVKUVCUVDJYDCTGYETGZYBTGZUVKUVCRYDCUUCSUWBYDYEUUEWIZVG
      ZYDYBYAYCWMSZCYEYBWNQWOWPWBYDUWCUVCTGZUVMUVHUVFRUWFYDUWBUWCUWGUWDUWFYEYBW
      QWHZUWAYBUVCUVDWRQYDUVHYEUUQHIZUVDKIZUVAYDUVGUWIUVDKYDUVGYEVRZUWIYDUVCYBK
      IZVRZUVGUWKYDUWGUWCUWMUVGRUWHUWFUVCYBWSPYDUWLYEYDUWBUWCUWLYERUWEUWFYEYBWT
      PXAXBUWIUUQYEHIUWKYEUUQUWDUUQMGZUUQTGZVQMGUWNXLVQXCXDZUUQXEXDZXFYEUWDXGXH
      XIWOYDUWBUWOUUSTGUVAUWJRUWEUWOYDUWQVGYDUUSUVTSYEUUQUUSXJQXMXKWOYDUUTTGZUV
      BUUTRZYDUUTMGZUWRYDUWNUVOUWTUWPUVSUUQUUSXNWHZUUTXEOUWRUWBUVQUWSUWDUVRUUTY
      EXOWFOXPUXAXQYPYLAYIDYMYIRZYOYKMUXBYNYJYELYMYIYBKXRWOXSXTP $.
      $( [13-May-2014] $)
  $}

  ${
    $d w x y z A $.  $d w x y C $.  $d x y F $.  $d w x y z ph $.  $d y z S $.
    $d w x y z D $.
    efif1o.1 $e |- F = ( w e. D |-> ( exp ` ( _i x. w ) ) ) $.
    efif1o.2 $e |- C = ( `' abs " { 1 } ) $.
    ${
      efif1olem3.3 $e |- ( ph -> D C_ RR ) $.
      efif1olem3.4 $e |- ( ( ph /\ ( x e. D /\ y e. D ) ) ->
                           ( abs ` ( x - y ) ) < ( 2 x. pi ) ) $.
      efif1olem3.5 $e |- ( ( ph /\ z e. RR ) ->
                           E. y e. D ( ( z - y ) / ( 2 x. pi ) ) e. ZZ ) $.
      efif1olem3.6 $e |- S = ( sin |` ( -u ( pi / 2 ) [,] ( pi / 2 ) ) ) $.
      $( The exponential function of an imaginary number maps any interval of
         length ` 2 pi ` one-to-one onto the unit circle.  (Contributed by Paul
         Chapman, 16-Mar-2008.)  (The proof was shortened by Mario Carneiro,
         13-May-2014.) $)
      efif1olem3 $p |- ( ph -> F : D -1-1-onto-> C ) $=
        ( wceq co wcel c1 syl wf1 wfo wf1o wf cv cfv wi wral ci cmul ce cr cabs
        sseld ccnv csn cima cc ax-icn recn mulcl sylancr efcl absefi wa wb absf
        wfn ffn ax-mp ax-1cn fniniseg mp2an sylanbrc syl6eleqr syl6 cmin cc0 c2
        fmptd cpi clt wbr wss simpll simplrl sseldd recnd simplrr subcl syl2anc
        cdiv wne 2re pire remulcli mp3an23 cle 0re ltleii adantr 1re pm3.2i cn0
        abscl cz divcan5 subdi syl3anc fveq2d simpr opreq2 fvex 3eqtr3d opreq1d
        a1i fvmpt eqtrd 3eqtrd efeq1 mpbid eqeltrrd wrex cneg cicc cexp sylancl
        simpld simprd sq11 renegcli csin ralrimiva opreq1 eleq1d caddc opreq12d
        3syl eqtr3d sqcl recni 2pos pipos mulgt0ii gt0ne0ii absdiv absid syl6eq
        opreq2i mulid1i ltdivmul mpbird eqbrtrd ine0 efsub efne0 divid nn0abscl
        syl6breqr nn0lt10b divcl abs00 diveq0 ex ralrimivva dff13 csqr syl6eleq
        subeq0 cim sylib sqrcl imcl absimle 2nn0 absexp sq1 syl6eqr absge0 lt01
        sqrth mpanr12 breqtrd absle elicc2 syl3anbrc cres resinf1o f1oeq1 mpbir
        f1ocnv f1of ffvelrni rehalfcl iccssre sseli remulcl rexbidv rcla4v sylc
        w3a ad2antrr efadd npcan mul12 mp3an12 2z efexp ccos sincossq absvalsq2
        2cn cre eqtr4d fveq1i fvres syl5eq f1ocnvfv2 sincl coscl pncan2 recoscl
        pncan cosq14ge0 sqrrege0 syl22anc opreq2d efival replim 3eqtr4d eqeq12d
        recl mulid2 syl5ib bitr2d adantl eqeq2d 3imtr4d reximdva dffo3 df-f1o
        mpd ) AGFIUAZGFIUBZGFIUCAGFIUDZBUEZIUFZCUEZIUFZPZVUFVUHPZUGZCGUHBGUHVUC
        AEGUIEUEZUJQZUKUFZFIAVUMGRVUMULRZVUOFRAGULVUMLUNVUPVUOUMUOSUPUQZFVUPVUO
        URRZVUOUMUFSPZVUOVUQRZVUPVUNURRZVURVUPUIURRZVUMURRVVAUSVUMUTUIVUMVAVBVU
        NVCTVUMVDUMURVHZSURRZVUTVURVUSVEVFURULUMUDVVCVGURULUMVIVJZVKURSVUOUMURV
        LVMVNKVOVPJVTZAVULBCGGAVUFGRZVUHGRZVEZVEZVUJVUKVVJVUJVEZVUFVUHVQQZVRPZV
        UKVVKVVLVSWAUJQZWLQZVRPZVVMVVKVVOUMUFZVRPZVVPVVKVVQSWBWCZVVRVVKVVQVVLUM
        UFZVVNWLQZSWBVVKVVQVVTVVNUMUFZWLQZVWAVVKVVLURRZVVQVWCPZVVKVUFURRZVUHURR
        ZVWDVVKVUFVVKGULVUFVVKAGULWDZAVVIVUJWELTZAVVGVVHVUJWFZWGWHZVVKVUHVVKGUL
        VUHVWIAVVGVVHVUJWIZWGWHZVUFVUHWJWKZVWDVVNURRZVVNVRWMZVWEVVNVSWAWNWOWPZU
        UAZVVNVWQVSWAWNWOUUBUUCUUDZUUEZVVLVVNUUFWQTVWBVVNVVTWLVVNULRZVRVVNWRWCV
        WBVVNPVWQVRVVNWSVWQVWSWTVVNUUGVMUUIUUHVVKVWASWBWCZVVTVVNSUJQZWBWCZVVKVV
        TVVNVXCWBVVJVVTVVNWBWCVUJMXAVVNVWRUUJUUSVVKVVTULRZVXBVXDVFZVVKVWDVXEVWN
        VVLXETVXESULRZVXAVRVVNWBWCZVEVXFXBVXAVXHVWQVWSXCVVTSVVNUUKWQTUULUUMVVKV
        VQXDRZVVSVVRVFVVKVVOXFRVXIVVKUIVVLUJQZUIVVNUJQZWLQZVVOXFVVKVWDVXLVVOPZV
        WNVWDVWOVWPVEZVVBUIVRWMZVEZVXMVWOVWPVWRVWTXCZVVBVXOUSUUNXCZVVLVVNUIXGWQ
        TVVKVXJUKUFZSPZVXLXFRZVVKVXSUIVUFUJQZUIVUHUJQZVQQZUKUFZVYBUKUFZVYCUKUFZ
        WLQZSVVKVXJVYDUKVVKVVBVWFVWGVXJVYDPVVBVVKUSXPVWKVWMUIVUFVUHXHXIXJVVKVYB
        URRZVYCURRZVYEVYHPVVKVVBVWFVYIUSVWKUIVUFVAVBVVKVVBVWGVYJUSVWMUIVUHVAZVB
        ZVYBVYCUUOWKVVKVYHVYGVYGWLQZSVVKVYFVYGVYGWLVVKVUGVUIVYFVYGVVJVUJXKVVKVV
        GVUGVYFPVWJEVUFVUOVYFGIVUMVUFPVUNVYBUKVUMVUFUIUJXLXJJVYBUKXMXQTVVKVVHVU
        IVYGPZVWLEVUHVUOVYGGIVUMVUHPVUNVYCUKVUMVUHUIUJXLXJJVYCUKXMXQZTXNXOVVKVY
        GURRZVYGVRWMZVYMSPVVKVYJVYPVYLVYCVCZTVVKVYJVYQVYLVYCUUPTVYGUUQWKXRXSVVK
        VXJURRZVXTVYAVFVVKVVBVWDVYSUSVWNUIVVLVAVBVXJXTTYAYBVVOUURTVVQUUTTYAVVKV
        VOURRZVVRVVPVFVVKVWDVYTVWNVWDVWOVWPVYTVWRVWTVVLVVNUVAWQTVVOUVBTYAVVKVWD
        VVPVVMVFZVWNVWDVWOVWPWUAVWRVWTVVLVVNUVCWQTYAVVKVWFVWGVVMVUKVFVWKVWMVUFV
        UHUVIWKYAUVDUVEBCGFIUVFVNAVUEVUFVUIPZCGYCZBFUHVUDVVFAWUCBFAVUFFRZVEZVSV
        UFUVGUFZUVJUFZHUOZUFZUJQZVUHVQQZVVNWLQZXFRZCGYCZWUCWUEWUJULRZDUEZVUHVQQ
        ZVVNWLQZXFRZCGYCZDULUHZWUNWUEVSULRWUIULRZWUOWNWUEWUGSYDZSYEQZRZWUIWAVSW
        LQZYDZWVFYEQZRZWVBWUEWUGULRZWVCWUGWRWCZWUGSWRWCZWVEWUEWUFURRZWVJWUEVWFW
        VMWUEVWFVUFUMUFZSPZWUEVUFVUQRZVWFWVOVEZWUEVUFFVUQAWUDXKKUVHVVCVVDWVPWVQ
        VFVVEVKURSVUFUMURVLVMUVKZYHZVUFUVLTZWUFUVMTZWUEWVKWVLWUEWUGUMUFZSWRWCZW
        VKWVLVEZWUEWWBWUFUMUFZSWRWUEWVMWWBWWEWRWCWVTWUFUVNTWUEWWEVSYFQZSVSYFQZP
        ZWWESPZWUEWWFSWWGWUEWUFVSYFQZUMUFZWVNWWFSWUEWWJVUFUMWUEVWFWWJVUFPWVSVUF
        UWATZXJWUEWVMVSXDRWWKWWFPWVTUVOWUFVSUVPYGWUEVWFWVOWVRYIXNZUVQUVRWUEWWEU
        LRZVRWWEWRWCZWWHWWIVFZWUEWVMWWNWVTWUFXETWUEWVMWWOWVTWUFUVSTWWNWWOVEVXGV
        RSWRWCWWPXBVRSWSXBUVTWTWWESYJUWBWKYAUWCWUEWVJVXGWWCWWDVFWWAXBWUGSUWDYGY
        AZYHWUEWVKWVLWWQYIWVCULRVXGWVEWVJWVKWVLUXAVFSXBYKXBWVCSWUGUWEVMUWFZWVDW
        VHWUGWUHWVDWVHWUHUCZWVDWVHWUHUDWVHWVDHUCZWWSWWTWVHWVDYLWVHUWGZUCZUWHHWX
        APWWTWXBVFOWVHWVDHWXAUWIVJUWJZWVHWVDHUWKVJWVDWVHWUHUWLVJUWMZWVHULWUIWVG
        ULRWVFULRZWVHULWDWVFWAULRWXEWOWAUWNVJZYKWXFWVGWVFUWOVMUWPYRZVSWUIUWQVBZ
        AWVAWUDAWUTDULNYMXAWUTWUNDWUJULWUPWUJPZWUSWUMCGWXIWURWULXFWXIWUQWUKVVNW
        LWUPWUJVUHVQYNXOYOUWRUWSUWTWUEWUMWUBCGWUEVVHVEZUIWUKUJQZUKUFZSPZVUFVYGP
        ZWUMWUBWXMWXLVYGUJQZSVYGUJQZPWXJWXNWXLSVYGUJYNWXJWXOVUFWXPVYGWXJWXOUIWU
        JUJQZUKUFZVUFWXJWXKVYCYPQZUKUFZWXOWXRWXJWXKURRZVYJWXTWXOPWXJVVBWUKURRZW
        YAUSWXJWUJURRZVWGWYBWXJWUJWUEWUOVVHWXHXAWHZWXJVUHWXJGULVUHAVWHWUDVVHLUX
        BWUEVVHXKWGWHZWUJVUHWJWKZUIWUKVAVBZWXJVVBVWGVYJUSWYEVYKVBZWXKVYCUXCWKWX
        JWXSWXQUKWXJWXSWXQVYCVQQZVYCYPQZWXQWXJWXKWYIVYCYPWXJVVBWYCVWGWXKWYIPVVB
        WXJUSXPWYDWYEUIWUJVUHXHXIXOWXJWXQURRZVYJWYJWXQPWXJVVBWYCWYKUSWYDUIWUJVA
        VBWYHWXQVYCUXDWKXRXJYSWUEWXRVUFPVVHWUEWXRUIWUIUJQZUKUFZVSYFQZWWJVUFWUEW
        XRVSWYLUJQZUKUFZWYNWUEWXQWYOUKWUEWUIURRZWXQWYOPZWUEWUIWXGWHZVVBVSURRWYQ
        WYRUSUXLUIVSWUIUXEUXFTXJWUEWYLURRZVSXFRWYPWYNPWUEVVBWYQWYTUSWYSUIWUIVAV
        BUXGWYLVSUXHYGXRWUEWYMWUFVSYFWUEWUIUXIUFZUIWUIYLUFZUJQZYPQZWUFUXMUFZUIW
        UGUJQZYPQZWYMWUFWUEXUAXUEXUCXUFYPWUEXUAVSYFQZXUEVSYFQZPZXUAXUEPZWUEXUBV
        SYFQZXUHYPQZXULVQQZXUIWUGVSYFQZYPQZXUOVQQZXUHXUIWUEXUMXUPXULXUOVQWUEXUM
        SXUPWUEWYQXUMSPWYSWUIUXJTWUEWWFXUPSWUEWVMWWFXUPPWVTWUFUXKTWWMYSUXNWUEXU
        BWUGVSYFWUEWUIHUFZXUBWUGWUEXURWUIWXAUFZXUBWUIHWXAOUXOWUEWVIXUSXUBPWUEWV
        EWVIWWRWXDTZWUIWVHYLUXPTUXQWUEWWTWVEXURWUGPWXCWWRWVHWVDWUGHUXRVBYSZXOZY
        QWUEXULURRZXUHURRZXUNXUHPWUEWYQXUBURRXVCWYSWUIUXSXUBYTYRZWUEWYQXUAURRXV
        DWYSWUIUXTXUAYTYRXULXUHUYAWKWUEXUIURRZXUOURRXUQXUIPWUEXUEURRXVFWUEXUEWU
        EWVMXUEULRZWVTWUFUYLTZWHXUEYTTWUEXULXUOURXVBXVEYBXUIXUOUYCWKXNWUEXUAULR
        ZVRXUAWRWCZXVGVRXUEWRWCZXUJXUKVFWUEWVBXVIWXGWUIUYBTWUEWVIXVJXUTWUIUYDTX
        VHWUEVWFXVKWVSVUFUYETXUAXUEYJUYFYAWUEXUBWUGUIUJXVAUYGYQWUEWYQWYMXUDPWYS
        WUIUYHTWUEWVMWUFXUGPWVTWUFUYITUYJXOWWLXSXAXRWXJVYPWXPVYGPWXJVYJVYPWYHVY
        RTVYGUYMTUYKUYNWXJWXMWXKVXKWLQZXFRZWUMWXJWYAWXMXVMVFWYGWXKXTTWXJXVLWULX
        FWXJWYBXVLWULPZWYFWYBVXNVXPXVNVXQVXRWUKVVNUIXGWQTYOUYOWXJVUIVYGVUFVVHVY
        NWUEVYOUYPUYQUYRUYSVUBYMCBGFIUYTVNGFIVUAVN $.
        $( [13-May-2014] $) $( [16-Mar-2008] $)
    $}

    efif1o.3 $e |- D = ( A (,] ( A + ( 2 x. pi ) ) ) $.
    $( The exponential function of an imaginary number maps any open-below,
       closed-above interval of length ` 2 pi ` one-to-one onto the unit
       circle.  (Contributed by Paul Chapman, 16-Mar-2008.) $)
    efif1o $p |- ( A e. RR -> F : D -1-1-onto-> C ) $=
      ( vx vy vz cr wcel csin cpi c2 cdiv co cneg wbr cicc cres cmul caddc cioc
      cv clt cle w3a wb 2re pire remulcli mpan2 elioc2 mpdan simp1 syl6bi ssrdv
      readdcl syl5eqss efif1olem1 efif1olem2 eqid efif1olem3 ) BLMZIJKACDNOPQRZ
      SVGUARUBZEFGVFDBBPOUCRZUDRZUERZLHVFIVKLVFIUFZVKMZVLLMZBVLUGTZVLVJUHTZUIZV
      NVFVJLMZVMVQUJVFVILMVRPOUKULUMBVIUTUNBVJVLUOUPVNVOVPUQURUSVAIJBDHVBJKBDHV
      CVHVDVE $.
      $( [13-May-2014] $) $( [16-Mar-2008] $)
  $}

  ${
    $d w x y z A $.  $d x y z C $.  $d x y z D $.  $d x y F $.  $d x y z ph $.
    $d y z S $.
    efifo.1 $e |- F = ( z e. RR |-> ( exp ` ( _i x. z ) ) ) $.
    efifo.2 $e |- C = ( `' abs " { 1 } ) $.
    $( The exponential function of an imaginary number maps the reals onto the
       unit circle.  (Contributed by Mario Carneiro, 13-May-2014.) $)
    efifo $p |- F : RR -onto-> C $=
      ( cr wfo wfn crn wceq wf ci co wcel cabs c1 cc ax-mp cc0 cioc df-fo cv ce
      wtru cmul cfv wi ccnv csn cima ax-icn recn mulcl sylancr syl absefi wa wb
      efcl absf ffn ax-1cn fniniseg mp2an sylanbrc syl6eleqr a1i fmptd trud wss
      frn cpi cres cmpt df-ima reseq1 clt wbr cle w3a 0re pire remulcli simp1bi
      2re elioc2 ssriv resmpt eqtri rneqi wf1o eqid caddc recni addid2i opreq2i
      c2 eqcomi efif1o f1ofo forn mp2b 3eqtri imassrn eqsstr3i eqssi mpbir2an )
      FBCGCFHZCIZBJFBCUAFBCKZXHXJUDAFLAUBZUEMZUCUFZBCXKFNZXMBNUGUDXNXMOUHPUIUJZ
      BXNXMQNZXMOUFPJZXMXONZXNXLQNZXPXNLQNXKQNXSUKXKULLXKUMUNXLUSUOXKUPOQHZPQNX
      RXPXQUQURQFOKXTUTQFOVARVBQPXMOQVCVDVEEVFVGDVHVIZFBCVARXIBXJXIBVJYAFBCVKRB
      CSWQVLUEMZTMZUJZXIYDCYCVMZIAYCXMVNZIZBCYCVOYEYFYEAFXMVNZYCVMZYFCYHJYEYIJD
      CYHYCVPRYCFVJYIYFJAYCFXKYCNZXNSXKVQVRZXKYBVSVRZSFNZYBFNYJXNYKYLVTURWAWQVL
      WEWBWCZSYBXKWFVDWDWGAFYCXMWHRWIWJYCBYFWKZYCBYFGYGBJYMYOWAASBYCYFYFWLESSYB
      WMMZTMYCYPYBSTYBYBYNWNWOWPWRWSRYCBYFWTYCBYFXAXBXCCYCXDXEXFXG $.
      $( [13-May-2014] $)
  $}

  ${
    $d x y C $.
    circgrp.1 $e |- C = ( `' abs " { 1 } ) $.
    circgrp.2 $e |- T = ( x. |` ( C X. C ) ) $.
    $( The circle group ` T ` is an Abelian group.  (Contributed by Paul
       Chapman, 25-Mar-2008.)  (Revised by Mario Carneiro, 13-May-2014.) $)
    circgrp $p |- T e. AbelOp $=
      ( vy vx cabl wcel wtru ci cr cv cmul co ce cfv cmpt wceq cc a1i wrex eqid
      crn cab wfo efifo forn ax-mp rnmpt eqtr3i wss ax-resscn readdsubg efghgrp
      ax-icn trud ) BGHIEFJABKEKJELMNOPZQZUCZAFLUQREKUAFUDKAURUEUSAREAURURUBZCU
      FKAURUGUHEFKUQURUTUIUJDJSHIUOTKSUKIULTUMUNUP $.
      $( [13-May-2014] $) $( [25-Mar-2008] $)
  $}

  ${
    $d w x y z D $.  $d x y z F $.  $d w x y z ph $.  $d x y S $.
    eff1olem.1 $e |- F = ( w e. D |-> ( exp ` ( _i x. w ) ) ) $.
    eff1olem.2 $e |- S = ( `' Im " D ) $.
    eff1olem.3 $e |- ( ph -> D C_ RR ) $.
    eff1olem.4 $e |- ( ( ph /\ ( x e. D /\ y e. D ) ) ->
                       ( abs ` ( x - y ) ) < ( 2 x. pi ) ) $.
    eff1olem.5 $e |- ( ( ph /\ z e. RR ) ->
                       E. y e. D ( ( z - y ) / ( 2 x. pi ) ) e. ZZ ) $.
    $( The exponential function maps the set ` S ` , of complex numbers with
       imaginary part in a real interval of length ` 2 x. pi ` , one-to-one
       onto the nonzero complex numbers.  (Contributed by Paul Chapman,
       16-Apr-2008.)  (The proof was shortened by Mario Carneiro,
       13-May-2014.) $)
    eff1olem $p |- ( ph -> ( exp |` S ) : S -1-1-onto-> ( CC \ { 0 } ) ) $=
      ( cc ce cfv cr co wceq wcel cc0 cdif cv cabs cres ccnv ci cdiv cmul caddc
      csn cmpt wfn eff2 ffn ax-mp dffn5v reseq1 sylbi wss cim cima cdm cnvimass
      wf imf fdmi eqcomi 3sstr4i resmpt eqtri sseli ffvelrni syl adantl crp clt
      wa wbr wne simpr eldifsn sylib simpld abscl cle 0re sylancr absge0 simprd
      wb ltlen abs00 necon3bid mpbird mpbir2and elrp sylanbrc wf1o reeff1o f1of
      f1ocnv mp2b recnd ax-icn adantr c1 csin c2 cneg cicc eqid efif1olem3 3syl
      cpi divcl syl3anc absdiv opreq2d divid syl2anc mulcl elpreima efadd fvres
      absidm f1ocnvfv2 eqtr3d opreq2 fveq2d fvex fvmpt eqeq2d syl5ibrcom eqtr4d
      opreq12d fveq2 f1ocnvfv1 eqtrd efcl 3eqtrd ax-1cn fniniseg ffvelrn sseldd
      absf mp2an addcl eqeltrd syl6eleqr cbvmptv divcan2 3eqtrrd adantrl wi cre
      crim replim absef recl imcl efne0 divcan3 simprbi eleq2s id adantrr f1o2d
      impbid ) ACBGNUAUKUBZCUCZOPZBUCZUDPZOQUEZUFZPZUGUVMUVNUHRZHUFZPZUIRZUJRZO
      GUEZUWCCNUVLULZGUEZCGUVLULZONUMZUWCUWESZNUVJOVEUWGUNNUVJOUOUPUWGOUWDSUWHC
      NOUQOUWDGURUSUPGNUTUWEUWFSVAUFFVBZVAVCZGNVAFVDJUWJNNQVAVFVGVHVIZCNGUVLVJU
      PVKUVKGTZUVLUVJTZAUWLUVKNTZUWMGNUVKUWKVLZNUVJUVKOUNVMVNVOAUVMUVJTZVRZUWBU
      WIGUWQUWBNTZUWBVAPZFTZUWBUWITZUWQUVQNTZUWANTZUWRUWQUVQUWQUVNVPTZUVQQTZUWQ
      UVNQTZUAUVNVQVSZUXDUWQUVMNTZUXFUWQUXHUVMUAVTZUWQUWPUXHUXIVRAUWPWAUVMNUAWB
      WCZWDZUVMWEVNZUWQUXGUAUVNWFVSZUVNUAVTZUWQUAQTUXFUXGUXMUXNVRWKWGUXLUAUVNWL
      WHUWQUXHUXMUXKUVMWIVNUWQUXNUXIUWQUXHUXIUXJWJUWQUVNUAUVMUAUWQUXHUVNUASUVMU
      ASWKUXKUVMWMVNWNWOZWPUVNWQWRZVPQUVNUVPQVPUVOWSZVPQUVPWSVPQUVPVEWTQVPUVOXB
      VPQUVPXAXCVMVNZXDZUWQUGNTZUVTNTUXCXEUWQUVTUWQFQUVTAFQUTUWPKXFUWQUDUFXGUKV
      BZFUVSVEZUVRUYATZUVTFTZAUYBUWPAFUYAHWSZUYAFUVSWSUYBABCDEUYAFXHXOXIUHRZXJU
      YFXKRUEZHIUYAXLKLMUYGXLXMZFUYAHXBUYAFUVSXAXNXFUWQUVRNTZUVRUDPZXGSZUYCUWQU
      XHUVNNTZUXNUYIUXKUWQUVNUXLXDZUXOUVMUVNXPXQUWQUYJUVNUVNUDPZUHRZUVNUVNUHRZX
      GUWQUXHUYLUXNUYJUYOSUXKUYMUXOUVMUVNXRXQUWQUYNUVNUVNUHUWQUXHUYNUVNSUXKUVMY
      FVNXSUWQUYLUXNUYPXGSUYMUXOUVNXTYAUUAUDNUMZXGNTUYCUYIUYKVRWKNQUDVEUYQUUFNQ
      UDUOUPUUBNXGUVRUDNUUCUUGWRZUYAFUVRUVSUUDYAZUUEZXDUGUVTYBWHZUVQUWAUUHYAUWQ
      UWSUVTFUWQUXEUVTQTUWSUVTSUXRUYTUVQUVTUUQYAUYSUUIVANUMZUXAUWRUWTVRWKNQVAVE
      VUBVFNQVAUOUPZNUWBFVAYCUPWRJUUJAUWLUWPVRVRZUVKUWBSZUVMUVLSZVUDVUFVUEUVMUW
      BOPZSZAUWPVUHUWLUWQVUGUVQOPZUWAOPZUIRZUVNUVRUIRZUVMUWQUXBUXCVUGVUKSUXSVUA
      UVQUWAYDYAUWQVUIUVNVUJUVRUIUWQUVQUVOPZVUIUVNUWQUXEVUMVUISUXRUVQQOYEVNUWQU
      XQUXDVUMUVNSWTUXPQVPUVNUVOYGWHYHUWQUVTHPZVUJUVRUWQUYDVUNVUJSUYSDUVTUGDUCZ
      UIRZOPZVUJFHVUOUVTSVUPUWAOVUOUVTUGUIYIYJHEFUGEUCZUIRZOPZULDFVUQULIEDFVUTV
      UQVURVUOSVUSVUPOVURVUOUGUIYIYJUUKVKUWAOYKYLVNUWQUYEUYCVUNUVRSAUYEUWPUYHXF
      UYRFUYAUVRHYGYAYHYPUWQUXHUYLUXNVULUVMSUXKUYMUXOUVMUVNUULXQUUMUUNVUEUVLVUG
      UVMUVKUWBOYQYMYNAUWLVUFVUEUUOUWPAUWLVRZVUEVUFUVKUVLUDPZUVPPZUGUVLVVBUHRZU
      VSPZUIRZUJRZSVVAUVKUVKUUPPZUGUVKVAPZUIRZUJRZVVGVVAUWNUVKVVKSUWLUWNAUWOVOZ
      UVKUURVNZVVAVVCVVHVVFVVJUJVVAVVCVVHUVOPZUVPPZVVHVVAVVBVVNUVPVVAVVBVVHOPZV
      VNVVAUWNVVBVVPSVVLUVKUUSVNZVVAVVHQTZVVNVVPSVVAUWNVVRVVLUVKUUTVNZVVHQOYEVN
      YOYJVVAUXQVVRVVOVVHSWTVVSQVPVVHUVOYRWHYSVVAVVEVVIUGUIVVAVVEVVIHPZUVSPZVVI
      VVAVVDVVTUVSVVAVVDVVJOPZVVTVVAVVDVVPVWBUIRZVVPUHRZVWBVVAUVLVWCVVBVVPUHVVA
      UVLVVKOPZVWCVVAUVKVVKOVVMYJVVAVVHNTZVVJNTZVWEVWCSVVAVVHVVSXDZVVAUXTVVINTV
      WGXEVVAVVIVVAUWNVVIQTVVLUVKUVAVNXDUGVVIYBWHZVVHVVJYDYAYSVVQYPVVAVWBNTZVVP
      NTZVVPUAVTZVWDVWBSVVAVWGVWJVWIVVJYTVNVVAVWFVWKVWHVVHYTVNVVAVWFVWLVWHVVHUV
      BVNVWBVVPUVCXQYSVVAVVIFTZVVTVWBSUWLVWMAVWMUVKUWIGUVKUWITZUWNVWMVUBVWNUWNV
      WMVRWKVUCNUVKFVAYCUPUVDJUVEVOZEVVIVUTVWBFHVURVVISVUSVVJOVURVVIUGUIYIYJIVV
      JOYKYLVNYOYJVVAUYEVWMVWAVVISAUYEUWLUYHXFVWOFUYAVVIHYRYAYSXSYPYOVUFUWBVVGU
      VKVUFUVQVVCUWAVVFUJVUFUVNVVBUVPUVMUVLUDYQZYJVUFUVTVVEUGUIVUFUVRVVDUVSVUFU
      VMUVLUVNVVBUHVUFUVFVWPYPYJXSYPYMYNUVGUVIUVH $.
      $( [13-May-2014] $) $( [16-Apr-2008] $)
  $}

  ${
    $d w x y z S $.
    eff1o.1 $e |- S = ( `' Im " ( -u pi (,] pi ) ) $.
    $( The exponential function maps the set ` S ` , of complex numbers with
       imaginary part in the closed-above, open-below interval from ` -u pi `
       to ` pi ` one-to-one onto the nonzero complex numbers.  (Contributed by
       Paul Chapman, 16-Apr-2008.)  (Revised by Mario Carneiro,
       13-May-2014.) $)
    eff1o $p |- ( exp |` S ) : S -1-1-onto-> ( CC \ { 0 } ) $=
      ( vx vy vz vw cpi cr wcel cc ce pire cioc co cmul wbr caddc recni opreq2i
      cv cneg cc0 csn cdif cres wf1o renegcli ci cfv cmpt wss wa clt cle elioc2
      eqid simp1 syl6bi ssrdv mpan2 c2 2timesi addcli addcomi cmin negsubi wceq
      w3a pncan mp2an eqtri 3eqtrri efif1olem1 efif1olem2 eff1olem ax-mp ) GUAZ
      HIZAJUBUCUDKAUEUFGLUGZVRCDEFVQGMNZAFVTUHFTONKUIUJZWAUPBVRGHIZVTHUKLVRWBUL
      ZCVTHWCCTZVTIWDHIZVQWDUMPZWDGUNPZVHWEVQGWDUOWEWFWGUQURUSUTCDVQVTGVQVAGONZ
      QNZVQMWIVQGGQNZQNWJVQQNZGWHWJVQQGGLRZVBSVQWJVQVSRGGWLWLVCZVDWKWJGVENZGWJG
      WMWLVFGJIZWOWNGVGWLWLGGVIVJVKVLSZVMDEVQVTWPVNVOVP $.
      $( [13-May-2014] $) $( [16-Apr-2008] $)
  $}

