$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Mapping of the exponential function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    efgh.1 $e |- F = { <. x , y >. | ( x e. CC /\
                                       y = ( exp ` ( A x. x ) ) ) } $.
    $( The exponential function of a scaled complex number is a group
       homomorphism from the group of complex numbers under addition to the set
       of complex numbers under multiplication.  (Contributed by Paul Chapman,
       25-Apr-2008.) $)
    efgh $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( F ` ( B + C ) ) = ( ( F ` B ) x. ( F ` C ) ) ) $=
      ( cc wcel w3a caddc co cmul ce cfv adddi fveq2d wceq wa efadd mulcl
      syl2an 3impdi eqtrd addcl cv opreq2 fvex fvopab4 syl 3adant1 opreqan12d
      3eqtr4d ) CHIZDHIZEHIZJZCDEKLZMLZNOZCDMLZNOZCEMLZNOZMLZURFOZDFOZEFOZMLZUQ
      UTVAVCKLZNOZVEUQUSVJNCDEPQUNUOUPVKVERZVAHIVCHIVLUNUOSUNUPSVAVCTCDUACEUAUB
      UCUDUOUPVFUTRZUNUOUPSURHIVMDEUEABURCAUFZMLZNOZUTHFVNURRVOUSNVNURCMUGQGUSN
      UHUIUJUKUOUPVIVERUNUOUPVGVBVHVDMABDVPVBHFVNDRVOVANVNDCMUGQGVANUHUIABEVPVD
      HFVNERVOVCNVNECMUGQGVCNUHUIULUKUM $.
      $( [25-Apr-2008] $)
  $}

  ${
    $d A x y $.  $d X x y $.
    efghgrpi.1 $e |- S = { y | E. x e. X y = ( exp ` ( A x. x ) ) } $.
    efghgrpi.2 $e |- G = ( x. |` ( S X. S ) ) $.
    efghgrpi.3 $e |- A e. CC $.
    efghgrpi.4 $e |- X C_ CC $.
    efghgrpi.5 $e |- ( + |` ( X X. X ) ) e. ( SubGrp ` + ) $.

    ${
      $d A w x y z $.  $d F w z $.  $d G w z $.  $d S w z $.  $d X w z $.
      efghgrpi.6 $e |- F = { <. x , y >. | ( x e. CC /\
                                             y = ( exp ` ( A x. x ) ) ) } $.
      $( Lemma for ~ efghgrpi , $)
      efghgrpilem $p |- G e. Abel $=
        ( vz vw caddc cabl wcel cxp cres csubg cfv cnaddabl wa subgabl cgr wi
        cc cmul wss w3a issubg mpbi 3simp1i axaddopr fdmi grprn cv co ce mulcl
        mpan efcl syl fopab ssid wf wfn axmulopr ffn ax-mp wceq efgh mp3an1
        cdm crn 3simp2i eqid resgrprn mp3an wrex cab cima fvex fnopab2 fnssres
        fnrnfv mp2an df-ima eqeq1 rexbidv opreq2 fveq2d eqeq2d cbvrexv syl6bb
        cbvabv fvres sseli fvopab4 eqtrd rexbiia abbii 3eqtr4i 3eqtr4ri
        ghsubgi pm3.27i ) PQRZPGGSTZPUAUBRZFQRZUCLXHXJUDXIQRZXKPXIUEFUFRXLXKUGN
        OUHXIEPFUIDUHUHGLPUHPUFRZXIUFRZXIPUJZXJXMXNXOUKLPXIULUMZUNUHUHSZUHPUOUP
        ZUQABUHUHCAURZUIUSZUTUBZEMXSUHRZXTUHRZYAUHRCUHRZYBYCJCXSVAVBXTVCVDVEUHV
        FXQUHUIVGUIXQVHVIXQUHUIVJVKYDNURZUHRZOURZUHRYEYGPUSEUBYEEUBZYGEUBUIUSVL
        JABCYEYGEMVMVNPVOXQVLXNGUHUJZGXIVPVLXRXMXNXOXPVQKPXIUHGXIVRVSVTEGTZVPZY
        GYEYJUBZVLZNGWAZOWBZEGWCDEUHVHZYIYKYOVLZABUHYAEXTUTWDMWEKYPYIUDYJGVHYQU
        HGEWFNOGYJWGVDWHEGWIBURZYAVLZAGWAZBWBYGCYEUIUSZUTUBZVLZNGWAZOWBDYOYTUUD
        BOYRYGVLZYTYGYAVLZAGWAUUDUUEYSUUFAGYRYGYAWJWKUUFUUCANGXSYEVLZYAUUBYGUUG
        XTUUAUTXSYECUIWLWMZWNWOWPWQHYNUUDOYMUUCNGYEGRZYLUUBYGUUIYLYHUUBYEGEWRUU
        IYFYHUUBVLGUHYEKWSABYEYAUUBUHEUUHMUUAUTWDWTVDXAWNXBXCXDXEIXFXGVDWH $.
        $( [25-Apr-2008] $)
    $}

    $( The image of a subgroup of the group ` + ` , under the exponential
       function of a scaled complex number, is an Abelian group.  (Contributed
       by Paul Chapman, 25-Apr-2008.) $)
    efghgrpi $p |- G e. Abel $=
      ( cv cc wcel cmul co ce cfv wceq wa copab eqid efghgrpilem ) ABCDALZMNBLC
      UDOPQRSTABUAZEFGHIJKUEUBUC $.
      $( [25-Apr-2008] $)
  $}

  ${
    $d F a z $.  $d S a $.  $d a x y $.
    efif.1 $e |- F = { <. x , y >. | ( x e. ( 0 [,) ( 2 x. pi ) ) /\
                       y = ( exp ` ( _i x. x ) ) ) } $.
    efif.2 $e |- S = { z e. CC | ( abs ` z ) = 1 } $.
    $( The exponential function of an imaginary number maps the closed-below,
       open-above interval from ` 0 ` to ` 2 x. pi ` to the unit circle.
       (Contributed by Paul Chapman, 16-Mar-2008.) $)
    efif $p |- F : ( 0 [,) ( 2 x. pi ) ) --> S $=
      ( va cc0 c2 cpi cmul co cico wf wfn cv cfv wcel wral ffnfv ci ce fvex
      fnopab2 cc cabs c1 wceq fveq2 eqeq1d elrab2 opreq2 fveq2d fvopab4 cr cle
      wbr clt w3a wb 0re 2re pire remulcli elico2 mp2an 3simp1bi recn axicn
      mulcl mpan efcl syl 3syl eqeltrd absefi eqtrd sylanbrc rgen mpbir2an ) IJ
      KLMZNMZDEOEWCPHQZERZDSZHWCTHWCDEUAABWCUBAQZLMZUCRZEWHUCUDFUEWFHWCWFWEUFSW
      EUGRZUHUIZWDWCSZCQZUGRZUHUIWKCWEUFDWMWEUIWNWJUHWMWEUGUJUKGULWLWEUBWDLMZUC
      RZUFABWDWIWPWCEWGWDUIWHWOUCWGWDUBLUMUNFWOUCUDUOZWLWDUPSZWDUFSZWPUFSZWLWRI
      WDUQURZWDWBUSURZIUPSWBUPSWLWRXAXBUTVAVBJKVCVDVEIWBWDVFVGVHZWDVIWSWOUFSZWT
      UBUFSWSXDVJUBWDVKVLWOVMVNVOVPWLWJWPUGRZUHWLWEWPUGWQUNWLWRXEUHUIXCWDVQVNVR
      VSVTWA $.
      $( [16-Mar-2008] $)
  $}

  ${
    $d C x $.  $d U c $.  $d U x $.
    efifolem1.1 $e |- C = sup ( { c e. ( 0 [,] pi ) |
      ( cos ` c ) = if ( U e. ( -u 1 (,) 1 ) , U , 0 ) } , RR , < ) $.
    $( Lemma for ~ efifo . $)
    efifolem1 $p |- ( U e. ( -u 1 (,) 1 ) ->
                      E. x e. ( 0 (,) pi ) U = ( cos ` x ) ) $=
      ( c1 cneg cioo co wcel ccos cfv wceq cv cc0 cpi wrex cif eqeq2 cc cicc
      crab 0re pire cr clt wbr w3a lt01 wb 1re lt0neg2 ax-mp mpbi renegcli cxr
      elioo2 rexr syl2an mp2an biimpri mp3an elimel 3simp1i pipos wss iccssre
      axresscn sstri ssid coscn sseli recoscl syl eqid cospi 3simp2i eqbrtri
      3simp3i cos0 breqtrri pm3.2i dsupivthi pm3.27i dedth eqcomd pm3.26i
      fveq2 eqeq2d rcla4ev mpan ) CFGZFHIZJZCBKLZMZCANZKLZMZAOPHIZQZWNWOCWNWOCM
      WOWNCORZMZCOCXBWOSBWTJZXCDOPBTDNZKLZXBMDOPUAIZUBZXBKUCUDXBUEJZWLXBUFUGZXB
      FUFUGZXBWMJZXIXJXKUHZCOWMOUEJZWLOUFUGZOFUFUGZOWMJZUCXPXOUIFUEJZXPXOUJUKFU
      LUMUNUIXQXNXOXPUHZWLUEJZXRXQXSUJZFUKUOZUKWLUPJZFUPJZYAXTXRWLFOUQWLURZFURZ
      USUTVAVBVCXTXRXLXMUJZYBUKYCYDYGXTXRWLFXBUQYEYFUSUTUNZVDVEXGUETXNPUEJXGUEV
      FUCUDOPVGUTZVHVITVJVKXEXGJXEUEJXFUEJXGUEXEYIVLXEVMVNXHVOPKLZXBUFUGXBOKLZU
      FUGYJWLXBUFVPXIXJXKYHVQVRXBFYKUFXIXJXKYHVSVTWAWBEWCZWDWEWFXDWPXAXDXCYLWGW
      SWPABWTWQBMWRWOCWQBKWHWIWJWKVN $.
      $( [16-Mar-2008] $)
  $}

  ${
    $d A a $.  $d X a $.  $d Y a $.
    efifolem2.1 $e |- A e. ( 0 (,) ( 2 x. pi ) ) $.
    efifolem2.2 $e |- X e. RR $.
    efifolem2.3 $e |- Y e. RR $.
    $( Lemma for ~ efifo . $)
    efifolem2 $p |- ( ( ( ( X ^ 2 ) + ( Y ^ 2 ) ) = 1 /\ X = ( cos ` A ) ) ->
      E. a e. ( 0 (,) ( 2 x. pi ) ) ( X = ( cos ` a ) /\ Y = ( sin ` a ) ) ) $=
      ( c2 cexp co caddc c1 wceq ccos cfv wa csin cneg wo cv cc0 cpi cmul cioo
      wrex cmin ax1cn recni sqcli subaddi biimpri opreq1 opreq2d sylan9req cc
      wcel cr clt wbr w3a wb 0re 2re pire remulcli cxr elioo2 rexr syl2an
      mp2an mpbi 3simp1i sincossq ax-mp coscl sincl subadd2i mpbir syl6eq
      sqeqori sylib ex ancld imp andi fveq2 eqeq2d anbi12d rcla4ev mpan
      3ancomb resubcli 2th posdifi ltsubpos 3anbi123i bitr3i 3bitr4i cos2pim
      eqeq2i sin2pim syl2anbr jaoi syl ) BHIJZCHIJZKJLMZBANOZMZPZXICAQOZMZPZXIC
      XKRZMZPZSZBDTZNOZMZCXRQOZMZPZDUAHUBUCJZUDJZUEZXJXIXLXOSZPZXQXGXIYHXGXIYGX
      GXIYGXJXFXKHIJZMYGXJXFLXHHIJZUFJZYIXGXIXFLXEUFJZYKYLXFMXGLXEXFUGBBFUHUICC
      GUHZUIUJUKXIXEYJLUFBXHHIULUMUNYKYIMYIYJKJLMZAUOUPZYNAAUQUPZUAAURUSZAYDURU
      SZAYEUPZYPYQYRUTZEUAUQUPZYDUQUPZYSYTVAZVBHUBVCVDVEZUAVFUPZYDVFUPZUUCUUAUU
      BUAYDAVGUAVHZYDVHZVIVJZVKVLZUHZAVMVNLYJYIUGXHYOXHUOUPUUKAVOVNUIXKYOXKUOUP
      UUKAVPVNZUIVQVRVSCXKYMUULVTWAWBWCWDXIXLXOWEWAXMYFXPYSXMYFEYCXMDAYEXRAMZXT
      XIYBXLUUMXSXHBXRANWFWGUUMYAXKCXRAQWFWGWHWIWJBYDAUFJZNOZMZCUUNQOZMZYFXIXOU
      UNYEUPZUUPUURPZYFYSUUSEYTUUNUQUPZUAUUNURUSZUUNYDURUSZUTZYSUUSYTYPYRYQUTUV
      DYPYRYQWKYPUVAYRUVBYQUVCYPUVAUUJYDAUUDUUJWLWMAYDUUJUUDWNYPUUBYQUVCVAUUJUU
      DAYDWOVJWPWQUUIUUAUUBUUSUVDVAZVBUUDUUEUUFUVEUUAUUBUAYDUUNVGUUGUUHVIVJWRVK
      YCUUTDUUNYEXRUUNMZXTUUPYBUURUVFXSUUOBXRUUNNWFWGUVFYAUUQCXRUUNQWFWGWHWIWJU
      UOXHBYOUUOXHMUUKAWSVNWTUUQXNCYOUUQXNMUUKAXAVNWTXBXCXD $.
      $( [16-Mar-2008] $)
  $}

  ${
    $d A a $.  $d X a z $.  $d Y a z $.  $d Z a $.
    $( Lemma for ~ efifo . $)
    efifolem3 $p |- ( ( X e. RR /\ Y e. RR /\ A e. ( 0 (,) ( 2 x. pi ) ) ) ->
      ( ( ( ( X ^ 2 ) + ( Y ^ 2 ) ) = 1 /\ X = ( cos ` A ) ) ->
    E. a e. ( 0 (,) ( 2 x. pi ) ) ( X = ( cos ` a ) /\ Y = ( sin ` a ) ) ) ) $=
      ( cr wcel cc0 c2 cpi cmul co cioo cexp caddc c1 wceq ccos cfv wa cv csin
      wrex wi cif opreq1 opreq1d eqeq1d eqeq1 anbi12d anbi1d rexbidv imbi12d
      opreq2d anbi2d fveq2 eqeq2d imbi1d clt wbr pire pipos recni mulid2i 1lt2
      1re 2re ltmul1ii mpbi eqbrtrri w3a wb 0re remulcli cxr elioo2 rexr
      syl2an mp2an biimpri mp3an elimel efifolem2 dedth3h ) BEFZCEFZAGHIJKZLKZF
      ZBHMKZCHMKZNKZOPZBAQRZPZSZBDTZQRZPZCWPUARZPZSZDWGUBZUCWDBGUDZHMKZWJNKZOPZ
      XCWMPZSZXCWQPZWTSZDWGUBZUCXDWECGUDZHMKZNKZOPZXGSZXIXLWSPZSZDWGUBZUCXOXCWH
      AIUDZQRZPZSZXSUCBCAGGIBXCPZWOXHXBXKYDWLXFWNXGYDWKXEOYDWIXDWJNBXCHMUEUFUGB
      XCWMUHUIYDXAXJDWGYDWRXIWTBXCWQUHUJUKULCXLPZXHXPXKXSYEXFXOXGYEXEXNOYEWJXMX
      DNCXLHMUEUMUGUJYEXJXRDWGYEWTXQXICXLWSUHUNUKULAXTPZXPYCXSYFXGYBXOYFWMYAXCA
      XTQUOUPUNUQXTXCXLDAIWGIEFZGIURUSZIWFURUSZIWGFZUTVAOIJKZIWFURIIUTVBVCOHURU
      SYKWFURUSVDOHIVEVFUTVAVGVHVIYJYGYHYIVJZGEFZWFEFZYJYLVKZVLHIVFUTVMGVNFWFVN
      FYOYMYNGWFIVOGVPWFVPVQVRVSVTWABGEVLWACGEVLWAWBWC $.
      $( [16-Mar-2008] $)

    $( Lemma for ~ efifo . $)
    efifolem4 $p |- ( ( X e. ( -u 1 (,) 1 ) /\ Y e. RR /\
                        ( ( X ^ 2 ) + ( Y ^ 2 ) ) = 1 ) ->
      E. a e. ( 0 [,) ( 2 x. pi ) ) ( X = ( cos ` a ) /\ Y = ( sin ` a ) ) ) $=
      ( vz c1 cneg cioo co wcel cr c2 cexp caddc wceq w3a cv ccos cfv csin wa
      cc0 cpi cmul wrex cico wi elioore efifolem3 exp3a 3expia com23 3impia
      r19.23adv cif cicc crab clt csup eqid efifolem1 wss 0re pire 2re
      remulcli cxr cle wbr recni mulid2i 1re 1lt2 ltleii wb pipos lemul1i
      ax-mp mpbi eqbrtrri iooss2 mpan2 rexr syl3an mp3an ssrexv syl syl5
      3expib com3r mpd 3impib ltle mpan imdistani anim1i 3impa elioo2 syl2an
      mp2an elico2 df-3an bitri 3imtr4i ssriv ) AEFZEGHIZBJIZAKLHBKLHMHENZOACPZ
      QRZNBXISRNTZCUAKUBUCHZGHZUDZXKCUAXLUEHZUDZXFXGXHXNXFAJIZXGXHTZXNUFAXEEUGX
      QXRXFXNXQXGXHXFXNUFXQXGXHOZADPZQRNZDXMUDZXNXFXSYAXNDXMXQXGXHXTXMIZYAXNUFZ
      UFXQXGTYCXHYDXQXGYCXHYDUFXQXGYCOXHYAXNXTABCUHUIUJUKULUMXFYADUAUBGHZUDZYBD
      XJXFAUAUNNCUAUBUOHUPJUQURZACYGUSUTYEXMVAZYFYBUFUAJIZUBJIZXLJIZYHVBVCKUBVD
      VCVEZUAVFIZUBVFIZXLVFIZYHYIYJYKYMYNYOOUBXLVGVHYHEUBUCHZUBXLVGUBUBVCVIVJEK
      VGVHZYPXLVGVHZEKVKVDVLVMUAUBUQVHYQYRVNVOEKUBVKVDVCVPVQVRVSUAUBXLVTWAUAWBZ
      UBWBXLWBZWCWDYADYEXMWEVQWFWGWHWIWJWKXMXOVAXNXPUFDXMXOXTJIZUAXTUQVHZXTXLUQ
      VHZOZUUAUAXTVGVHZTZUUCTZYCXTXOIZUUAUUBUUCUUGUUAUUBTUUFUUCUUAUUBUUEYIUUAUU
      BUUEUFVBUAXTWLWMWNWOWPYIYKYCUUDVNZVBYLYMYOUUIYIYKUAXLXTWQYSYTWRWSUUHUUAUU
      EUUCOZUUGYIYKUUHUUJVNVBYLUAXLXTWTWSUUAUUEUUCXAXBXCXDXKCXMXOWEVQWF $.
      $( [16-Mar-2008] $)

    $( Lemma for ~ efifo . $)
    efifolem5 $p |- ( ( X e. RR /\ Y e. RR /\ ( ( X ^ 2 ) + ( Y ^ 2 ) ) = 1 )
      -> ( Y =/= 0 -> E. a e. ( 0 [,) ( 2 x. pi ) )
                         ( X = ( cos ` a ) /\ Y = ( sin ` a ) ) ) ) $=
      ( cr wcel c2 cexp co caddc c1 wceq cc0 wne cv ccos cfv csin wa cpi cmul
      cico wrex wi w3a cneg cioo cmin wb cc ax1cn subadd mp3an1 recn syl2an
      resqcl 3adant3 clt wbr breq2 sqgt0 syl5cbir 3adant1 3simp1 jctild 1re
      posdif mpan2 syl biimprd imdistani cabs cle 0re lt01 ltleii lt2sq
      mpanr12 abscl absge0 sylanc abslt absresq sq1 a1i breq12d 3bitr3rd
      pm5.32i renegcli cxr elioo2 rexr mp2an biimpri 3expb sylbi syl6 sylbird
      efifolem4 3expib com12 ex 3ad2ant2 mpdd 3expia com23 3impia ) ADEZBDEZAFG
      HZBFGHZIHJKZBLMZACNZOPKBXMQPKRCLFSTHUAHUBZUCXGXHRXLXKXNXGXHXLXKXNUCXGXHXL
      UDZXKAJUEZJUFHEZXNXOXKJXIUGHZXJKZXQXGXHXSXKUHZXLXIDEZXJDEZXTXGXHXIUIEZXJU
      IEZXTYAYBJUIEYCYDXTUJJXIXJUKULXIUMXJUMUNAUOZBUOUNUPXOXSXGLXRUQURZRZXQXOXS
      YFXGXHXLXSYFUCXGXSYFLXJUQURXHXLRXRXJLUQUSBUTVAVBXGXHXLVCVDYGXGXIJUQURZRZX
      QXGYFYHXGYHYFXGYAYHYFUHZYEYAJDEZYJVEXIJVFVGVHVIVJYIXGXPAUQURZAJUQURZRZRXQ
      XGYHYNXGAVKPZJUQURZYOFGHZJFGHZUQURZYNYHXGAUIEZYPYSUHZAUMYODEZLYOVLURZUUAY
      TUUBUUCRYKLJVLURUUAVELJVMVEVNVOYOJVPVQAVRAVSVTVHXGYKYPYNUHVEAJWAVGXGYQXIY
      RJUQAWBYRJKXGWCWDWEWFWGXGYLYMXQXQXGYLYMUDZXPDEZYKXQUUDUHZJVEWHVEXPWIEJWIE
      UUFUUEYKXPJAWJXPWKJWKUNWLWMWNWOVHWPWQXHXGXKXQXNUCZUCXLXHXKUUGXQXHXKRXNXQX
      HXKXNABCWRWSWTXAXBXCXDXEXF $.
      $( [16-Mar-2008] $)

    $( Lemma for ~ efifo . $)
    efifolem6 $p |- ( ( X e. RR /\ Y e. RR /\ ( ( X ^ 2 ) + ( Y ^ 2 ) ) = 1 )
       -> ( Y = 0 -> E. a e. ( 0 [,) ( 2 x. pi ) )
                     ( X = ( cos ` a ) /\ Y = ( sin ` a ) ) ) ) $=
      ( cr wcel c2 cexp co caddc c1 wceq cc0 cv ccos cfv csin wa cpi cmul cico
      wrex wi cneg wo wb sq0i opreq2d cc recn sqcl addid1 3syl sylan9eqr
      eqeq1d ax1cn sqeqor mpan2 syl sq1 eqeq2i syl5bbr adantr bitrd ex
      pm5.32rd cle wbr clt 0re leidi 2re pire 2pos pipos mulgt0ii w3a remulcli
      elico2 mp2an biimpri mp3an fveq2 eqeq2d anbi12d rcla4ev mpan cos0 eqeq2
      mpbiri eqcomd sin0 syl2an ltleii recni mulid2i 1lt2 1re ltmul1ii mpbi
      eqbrtrri cospi sinpi jaoian syl6bi 3impib 3expia 3adant2 ) ADEZAFGHZBFGHZ
      IHZJKZBLKZACMZNOZKZBXNPOZKZQZCLFRSHZTHZUAZUBBDEXHXLXMYBXHXLXMYBXHXLXMQAJK
      ZAJUCZKZUDZXMQYBXHXMXLYFXHXMXLYFUEXHXMQZXLXIJKZYFYGXKXIJXMXHXKXILIHZXIXMX
      JLXIIBUFUGXHAUHEZXIUHEYIXIKAUIZAUJXIUKULUMUNXHYHYFUEXMXHXIJFGHZKZYFYHXHYJ
      YMYFUEZYKYJJUHEYNUOAJUPUQURYLJXIUSUTVAVBVCVDVEYCXMYBYEALNOZKZBLPOZKZYBYCX
      MLYAEZYPYRQZYBLDEZLLVFVGZLXTVHVGZYSVILVIVJFRVKVLVMVNVOYSUUAUUBUUCVPZUUAXT
      DEZYSUUDUEVIFRVKVLVQZLXTLVRVSVTWAXSYTCLYAXNLKZXPYPXRYRUUGXOYOAXNLNWBWCUUG
      XQYQBXNLPWBWCWDWEWFYCYOAYCYOAKYOJKWGAJYOWHWIWJXMYQBXMYQBKYQLKWKBLYQWHWIWJ
      WLARNOZKZBRPOZKZYBYEXMRYAEZUUIUUKQZYBRDEZLRVFVGZRXTVHVGZUULVLLRVIVLVNWMJR
      SHZRXTVHRRVLWNWOJFVHVGUUQXTVHVGWPJFRWQVKVLVNWRWSWTUULUUNUUOUUPVPZUUAUUEUU
      LUURUEVIUUFLXTRVRVSVTWAXSUUMCRYAXNRKZXPUUIXRUUKUUSXOUUHAXNRNWBWCUUSXQUUJB
      XNRPWBWCWDWEWFYEUUHAYEUUHAKUUHYDKXAAYDUUHWHWIWJXMUUJBXMUUJBKUUJLKXBBLUUJW
      HWIWJWLXCXDXEXFXG $.
      $( [16-Mar-2008] $)

    $( Lemma for ~ efifo . $)
    efifolem7 $p |- ( ( Z e. CC /\ ( abs ` Z ) = 1 ) ->
                 E. a e. ( 0 [,) ( 2 x. pi ) ) Z = ( exp ` ( _i x. a ) ) ) $=
      ( cc wcel cabs cfv c1 wceq wa ci cv cmul co ce cc0 c2 cpi cico wrex cre
      ccos cim csin cexp cr cle wbr wb 1re 0re lt01 ltleii sq11 mpanr12 abscl
      absge0 sylanc biimpar sq1 syl6eq caddc w3a efifolem6 efifolem5 pm2.61dne
      recl adantr imcl absvalsq2 eqeq1d biimpa syl3anc syldan replim recn
      efival syl eqeqan12d cru jca recoscl resincl syl2an bitrd clt 2re pire
      remulcli elico2 mp2an 3simp1bi sylan2 rexbidva mpbird ) ACDZAEFZGHZIZAJBK
      ZLMNFZHZBOPQLMZRMZSZATFZWSUAFZHAUBFZWSUCFZHIZBXCSZWOWQWPPUDMZGHZXJWRXKGPU
      DMZGWOXKXMHZWQWPUEDZOWPUFUGZXNWQUHZWOXOXPIGUEDOGUFUGXQUIOGUJUIUKULWPGUMUN
      AUOAUPUQURUSUTXEUEDZXGUEDZXEPUDMXGPUDMVAMZGHZXJWOXLIXRXSYAVBXJXGOXEXGBVCX
      EXGBVDVEWOXRXLAVFZVGWOXSXLAVHZVGWOXLYAWOXKXTGAVIVJVKVLVMWOXDXJUHWQWOXAXIB
      XCWOWSUEDZXAXIUHWSXCDZWOYDIXAXEJXGLMVAMZXFJXHLMVAMZHZXIWOYDAYFWTYGAVNYDWS
      CDWTYGHWSVOWSVPVQVRXRXSIXFUEDZXHUEDZIYHXIUHWOYDXEXGXFXHVSWOXRXSYBYCVTYDYI
      YJWSWAWSWBVTWCWDYEYDOWSUFUGZWSXBWEUGZOUEDXBUEDYEYDYKYLVBUHUJPQWFWGWHOXBWS
      WIWJWKWLWMVGWN $.
      $( [16-Mar-2008] $)
  $}

  ${
    $d F a w $.  $d F w z $.  $d S a w $.  $d a x y $.
    efifo.1 $e |- F = { <. x , y >. | ( x e. ( 0 [,) ( 2 x. pi ) ) /\
                        y = ( exp ` ( _i x. x ) ) ) } $.
    efifo.2 $e |- S = { z e. CC | ( abs ` z ) = 1 } $.
    $( The exponential function of an imaginary number maps the closed-below,
       open-above interval from ` 0 ` to ` 2 x. pi ` onto the unit circle.
       (Contributed by Paul Chapman, 16-Mar-2008.) $)
    efifo $p |- F : ( 0 [,) ( 2 x. pi ) ) -onto-> S $=
      ( vw va cc0 c2 cpi cmul co cico wfo wf cv cfv wceq wrex wral dffo3 efif
      cc wcel cabs c1 wa ci ce efifolem7 fveq2 eqeq1d elrab2 opreq2 fveq2d
      fvex fvopab4 eqeq2d rexbiia 3imtr4i rgen mpbir2an ) JKLMNONZDEPVEDEQHRZIR
      ZESZTZIVEUAZHDUBIHVEDEUCABCDEFGUDVJHDVFUEUFVFUGSZUHTZUIVFUJVGMNZUKSZTZIVE
      UAVFDUFVJVFIULCRZUGSZUHTVLCVFUEDVPVFTVQVKUHVPVFUGUMUNGUOVIVOIVEVGVEUFVHVN
      VFABVGUJARZMNZUKSVNVEEVRVGTVSVMUKVRVGUJMUPUQFVMUKURUSUTVAVBVCVD $.
      $( [16-Mar-2008] $)
  $}

  $( Lemma for ~ efif1 . $)
  efif1lem1 $p |- ( A e. ( pi (,) ( 2 x. pi ) ) ->
                    ( A - pi ) e. ( 0 (,) pi ) ) $=
    ( cr wcel cpi clt wbr c2 cmul co w3a cmin cc0 cioo pire resubcl mpan2
    3ad2ant1 wb posdif mpan biimpa 3adant3 caddc ltsubadd mp3an23 recni
    2timesi breq2i syl6bbr biimpar 3adant2 3jca 2re remulcli cxr elioo2 rexr
    syl2an mp2an 0re 3imtr4i ) ABCZDAEFZAGDHIZEFZJZADKIZBCZLVGEFZVGDEFZJZADVDMI
    CZVGLDMICZVFVHVIVJVBVCVHVEVBDBCZVHNADOPQVBVCVIVEVBVCVIVNVBVCVIRNDASTUAUBVBV
    EVJVCVBVJVEVBVJADDUCIZEFZVEVBVNVNVJVPRNNADDUDUEVDVOAEDDNUFUGUHUIUJUKULVNVDB
    CZVLVFRZNGDUMNUNDUOCZVDUOCVRVNVQDVDAUPDUQZVDUQURUSLBCZVNVMVKRZUTNLUOCVSWBWA
    VNLDVGUPLUQVTURUSVA $.
    $( [16-Mar-2008] $)

  $( Lemma for ~ efif1 . $)
  efif1lem2 $p |- ( A e. ( pi [,) ( 2 x. pi ) ) ->
                    ( A - pi ) e. ( 0 [,) pi ) ) $=
    ( cr wcel cpi cle wbr c2 cmul co clt w3a cmin cc0 cico pire resubcl mpan2
    3ad2ant1 wb subge0 biimpar 3adant3 caddc ltsubadd mp3an23 recni 2timesi
    breq2i syl6bbr 3adant2 3jca 2re remulcli elico2 mp2an 0re 3imtr4i ) ABCZDAE
    FZAGDHIZJFZKZADLIZBCZMVCEFZVCDJFZKZADUTNICZVCMDNICZVBVDVEVFURUSVDVAURDBCZVD
    OADPQRURUSVEVAURVEUSURVJVEUSSOADTQUAUBURVAVFUSURVFVAURVFADDUCIZJFZVAURVJVJV
    FVLSOOADDUDUEUTVKAJDDOUFUGUHUIUAUJUKVJUTBCVHVBSOGDULOUMDUTAUNUOMBCVJVIVGSUP
    OMDVCUNUOUQ $.
    $( [16-Mar-2008] $)

  $( Lemma for ~ efif1 . $)
  efif1lem3 $p |- ( ( A e. ( 0 [,) pi ) /\ B e. ( 0 [,) pi ) ) ->
                ( ( exp ` ( _i x. A ) ) = ( exp ` ( _i x. B ) ) -> A = B ) ) $=
    ( cc0 cpi cico co wcel wa ci cmul ce cfv wceq ccos csin caddc cr cc cle
    wbr clt w3a wb 0re pire elico2 mp2an 3simp1bi recn efival 3syl eqeqan12d
    cru recoscl resincl jca syl syl2an biimpd sylbid pm3.26 syl6 cosh111
    sylibrd ) ACDEFZGZBVEGZHZIAJFKLZIBJFKLZMZANLZBNLZMZABMVHVKVNAOLZBOLZMZHZVNV
    HVKVLIVOJFPFZVMIVPJFPFZMZVRVFVGVIVSVJVTVFAQGZARGVIVSMVFWBCASTZADUATZCQGZDQG
    ZVFWBWCWDUBUCUDUECDAUFUGUHZAUIAUJUKVGBQGZBRGVJVTMVGWHCBSTZBDUATZWEWFVGWHWIW
    JUBUCUDUECDBUFUGUHZBUIBUJUKULVHWAVRVLQGZVOQGZHZVMQGZVPQGZHZWAVRUCVFVGVLVOVM
    VPUMVFWBWNWGWBWLWMAUNAUOUPUQVGWHWQWKWHWOWPBUNBUOUPUQURUSUTVNVQVAVBABVCVD $.
    $( [16-Mar-2008] $)

  $( Lemma for ~ efif1 . $)
  efif1lem4 $p |- ( ( A e. ( pi [,) ( 2 x. pi ) ) /\
                      B e. ( pi [,) ( 2 x. pi ) ) ) ->
      ( ( exp ` ( _i x. A ) ) = ( exp ` ( _i x. B ) ) -> A = B ) ) $=
    ( cpi c2 cmul co cico wcel wa ci ce cfv cneg wceq cmin cc cr cle wbr clt
    w3a wb pire 2re remulcli elico2 mp2an 3simp1bi recnd efimpi syl eqeqan12d
    cc0 wi efif1lem3 efif1lem2 syl2an sylbird neg11 axicn mulcl mpan efcl 3syl
    recni subcan2 mp3an3 3imtr3d ) ACDCEFZGFZHZBVJHZIZJAEFZKLZMZJBEFZKLZMZNZACO
    FZBCOFZNZVOVRNZABNZVMVTJWAEFKLZJWBEFKLZNZWCVKVLWFVPWGVSVKAPHZWFVPNVKAVKAQHZ
    CARSZAVITSZCQHZVIQHZVKWJWKWLUAUBUCDCUDUCUEZCVIAUFUGUHUIZAUJUKVLBPHZWGVSNVLB
    VLBQHZCBRSZBVITSZWMWNVLWRWSWTUAUBUCWOCVIBUFUGUHUIZBUJUKULWAUMCGFZHWBXBHWHWC
    UNVKVLWAWBUOAUPBUPUQURVOPHZVRPHZVTWDUBVKVLVOVRUSVKWIVNPHZXCWPJPHZWIXEUTJAVA
    VBVNVCVDVLWQVQPHZXDXAXFWQXGUTJBVAVBVQVCVDUQWIWQWCWEUBZVKVLWIWQCPHXHCUCVEABC
    VFVGWPXAUQVH $.
    $( [16-Mar-2008] $)

  ${
    efif1lem5.1 $e |- A e. ( 0 [,) pi ) $.
    efif1lem5.2 $e |- B e. ( pi [,) ( 2 x. pi ) ) $.
    $( Lemma for ~ efif1 . $)
    efif1lem5 $p |- -. ( exp ` ( _i x. A ) ) = ( exp ` ( _i x. B ) ) $=
      ( cc0 wceq cpi cioo co wcel wo c2 cmul ci ce cfv wn csn cico cun cr clt
      wbr 0re pire pipos snunioo mp3an eleq2i elun bitr3i mpbi wb elsnc2g
      ax-mp orbi1i 2re remulcli c1 1lt2 ltmulgt12 wa ccos wne ax1ne0 df-ne
      cneg fveq2 cos0 syl6eq cospi eqeqan12d ax1cn eqnegi syl6bb mtbiri csin
      orc ianor sylibr cle w3a elico2 mp2an 3simp1i efieq notbii syl
      sinq12gt0t resincl gt0ne0i sylib adantr eqtr sinpi adantl sylan2 expcom
      mtod olc cmin efif1lem1 cc recni sinmpi breq2i lt0neg1 bitr4i ltnei sin0
      syl6req sylan mtand ltnsymi wi breq2 syl5cbi ccase ) AEFZAEGHIZJZKZBGFZBG
      LGMIZHIZJZKZNAMIOPNBMIOPFZQZAERZJZYAKZYBAEGSIZJZYLCYNAYJXTTZJYLYOYMAEUAJZ
      GUAJZEGUBUCZYOYMFUDUEUFEGUGUHUIAYJXTUJUKULYKXSYAYPYKXSUMUDAEUAUNUOUPULBGR
      ZJZYFKZYGBGYDSIZJZUUADUUCBYSYETZJUUAUUDUUBBYQYDUAJZGYDUBUCZUUDUUBFUELGUQU
      EURZUSLUBUCZUUFUTYQLUAJYRUUHUUFUMUEUQUFGLVAUHULGYDUGUHUIBYSYEUJUKULYTYCYF
      YQYTYCUMUEBGUAUNUOUPULXSYCYAYFYIXSYCVBZAVCPZBVCPZFZQZYIUUIUULUSEFZUSEVDUU
      NQVEUSEVFULUUIUULUSUSVGZFUUNXSYCUUJUSUUKUUOXSUUJEVCPUSAEVCVHVIVJYCUUKGVCP
      UUOBGVCVHVKVJVLUSVMVNVOVPUUMUULAVQPZBVQPZFZVBZQZYIUUMUUMUURQZKZUUTUUMUVAV
      RUULUURVSZVTYHUUSAUAJZBUAJZYHUUSUMUVDEAWAUCZAGUBUCZYNUVDUVFUVGWBZCYPYQYNU
      VHUMUDUEEGAWCWDULWEZUVEGBWAUCZBYDUBUCZUUCUVEUVJUVKWBZDYQUUEUUCUVLUMUEUUGG
      YDBWCWDULWEZABWFWDWGZVTWHYAYCVBZUVAYIUVOUURUUPEFZYAUVPQZYCYAUUPEVDZUVQYAE
      UUPUBUCZUVRAWIZUUPUVDUUPUAJUVIAWJUOWKWHUUPEVFWLWMUURUVOUVPUURUUQEFZUVPUVO
      UUPUUQEWNYCUWAYAYCUUQGVQPEBGVQVHWOVJWPWQWRWSUVAUUTYIUVAUVBUUTUVAUUMWTUVCV
      TUVNVTZWHXSYFVBZUVAYIUWCUUREUUQFZYFUWDQZXSYFEUUQVDZUWEYFUUQEUBUCZUWFYFEBG
      XAIZVQPZUBUCZUWGYFUWHXTJUWJBXBUWHWIWHUWJEUUQVGZUBUCZUWGUWIUWKEUBBXCJUWIUW
      KFBUVMXDBXEUOXFUVEUWGUWLUMZUVMUVEUUQUAJZUWMBWJZUUQXGWHUOXHWLZUUQEUVEUWNUV
      MUWOUOZUDXIWHEUUQVFWLWPEUUPFZUURUWDUWCEUUPUUQWNXSUWRYFXSUUPEVQPEAEVQVHXJX
      KWMXLXMUWBWHYAYFVBZUVAYIUWSUUREUUQUBUCZYFUWTQZYAYFUWGUXAUWPUUQEUWQUDXNWHW
      PYAUURUWTXOYFUURUVSUWTYAUUPUUQEUBXPUVTXQWMWSUWBWHXRWD $.
      $( [16-Mar-2008] $)
  $}

  $( Lemma for ~ efif1 . $)
  efif1lem6 $p |- ( ( A e. ( 0 [,) pi ) /\ B e. ( pi [,) ( 2 x. pi ) ) ) ->
                     -. ( exp ` ( _i x. A ) ) = ( exp ` ( _i x. B ) ) ) $=
    ( cc0 cpi cico co wcel c2 cmul ci ce cfv wceq wn cif opreq2 fveq2d eqeq1d
    notbid eqeq2d cr cle wbr clt 0re leidi pipos w3a wb pire elico2 mp2an
    biimpri mp3an elimel c1 1lt2 2re ltmulgt12 mpbi remulcli efif1lem5 dedth2h
    ) ACDEFZGZBDHDIFZEFZGZJAIFZKLZJBIFZKLZMZNJVEACOZIFZKLZVLMZNVPJVHBDOZIFZKLZM
    ZNABCDAVNMZVMVQWBVJVPVLWBVIVOKAVNJIPQRSBVRMZVQWAWCVLVTVPWCVKVSKBVRJIPQTSVNV
    RACVDCUAGZCCUBUCZCDUDUCZCVDGZUECUEUFUGWGWDWEWFUHZWDDUAGZWGWHUIUEUJCDCUKULUM
    UNUOBDVGWIDDUBUCZDVFUDUCZDVGGZUJDUJUFUPHUDUCZWKUQWIHUAGWFWMWKUIUJURUGDHUSUN
    UTWLWIWJWKUHZWIVFUAGWLWNUIUJHDURUJVADVFDUKULUMUNUOVBVC $.
    $( [16-Mar-2008] $)

  $( Lemma for ~ efif1 . $)
  efif1lem7 $p |- ( ( A e. ( 0 [,) ( 2 x. pi ) ) /\
                       B e. ( 0 [,) ( 2 x. pi ) ) ) ->
        ( ( exp ` ( _i x. A ) ) = ( exp ` ( _i x. B ) ) -> A = B ) ) $=
    ( cc0 cpi cico co wcel c2 cmul wo ci ce cfv wceq wi efif1lem3 wa wn
    efif1lem6 ancoms eqcom notbii sylib pm2.21d efif1lem4 ccase cun cr w3a cle
    wbr 0re pire 2re remulcli 3pm3.2i pipos ltleii c1 clt 1lt2 wb ltmulgt12
    mp3an mpbi pm3.2i icoun mp2an eleq2i elun bitr3i syl2anb ) ACDEFZGZADHDIFZE
    FZGZJZBVMGZBVPGZJZKAIFLMZKBIFLMZNZABNZOZACVOEFZGZBWGGZVNVSVQVTWFABPVQVSQZWD
    WEWJWCWBNZRZWDRVSVQWLBASTWKWDWCWBUAUBUCUDVNVTQWDWEABSUDABUEUFWHAVMVPUGZGVRW
    MWGACUHGZDUHGZVOUHGZUICDUJUKZDVOUJUKZQWMWGNWNWOWPULUMHDUNUMUOZUPWQWRCDULUMU
    QURDVOUMWSUSHUTUKZDVOUTUKZVAWOHUHGCDUTUKWTXAVBUMUNUQDHVCVDVEURVFCDVOVGVHZVI
    AVMVPVJVKWIBWMGWAWMWGBXBVIBVMVPVJVKVL $.
    $( [16-Mar-2008] $)

  ${
    $d F a b $.  $d F z $.  $d a b x y $.
    efif1.1 $e |- F = { <. x , y >. | ( x e. ( 0 [,) ( 2 x. pi ) ) /\
                        y = ( exp ` ( _i x. x ) ) ) } $.
    efif1.2 $e |- S = { z e. CC | ( abs ` z ) = 1 } $.
    $( The exponential function of an imaginary number maps the closed-below,
       open-above interval from ` 0 ` to ` 2 x. pi ` one-to-one to the unit
       circle.  (Contributed by Paul Chapman, 16-Mar-2008.) $)
    efif1 $p |- F : ( 0 [,) ( 2 x. pi ) ) -1-1-> S $=
      ( va vb cc0 c2 cpi cmul co cico wf1 wf cv cfv wceq wi wral dff13 efif
      wcel wa ci ce opreq2 fveq2d fvex fvopab4 eqeqan12d efif1lem7 sylbid
      rgen2a mpbir2an ) JKLMNONZDEPURDEQHRZESZIRZESZTZUSVATZUAZIURUBHURUBHIURDE
      UCABCDEFGUDVEHIURUSURUEZVAURUEZUFVCUGUSMNZUHSZUGVAMNZUHSZTVDVFVGUTVIVBVKA
      BUSUGARZMNZUHSZVIUREVLUSTVMVHUHVLUSUGMUIUJFVHUHUKULABVAVNVKUREVLVATVMVJUH
      VLVAUGMUIUJFVJUHUKULUMUSVAUNUOUPUQ $.
      $( [16-Mar-2008] $)

    $( The exponential function of an imaginary number maps the closed-below,
       open-above interval from ` 0 ` to ` 2 x. pi ` one-to-one onto the unit
       circle.  (Contributed by Paul Chapman, 16-Mar-2008.) $)
    efif1o $p |- F : ( 0 [,) ( 2 x. pi ) ) -1-1-onto-> S $=
      ( cc0 c2 cpi cmul co cico wf1o wf1 wfo df-f1o efif1 efifo mpbir2an ) HIJK
      LMLZDENUADEOUADEPUADEQABCDEFGRABCDEFGST $.
      $( [16-Mar-2008] $)
  $}

  ${
    $d A w $.  $d C x z $.  $d w x y z $.
    circgrp.1 $e |- C = { w e. CC | ( abs ` w ) = 1 } $.
    $( Membership of the exponential function of ` _i ` times a real number in
       the unit circle.  (Contributed by Paul Chapman, 25-Mar-2008.) $)
    efielcirc $p |- ( A e. RR -> ( exp ` ( _i x. A ) ) e. C ) $=
      ( ci cmul co ce cfv wcel cc cabs c1 wceq cr cv fveq2 eqeq1d elrab2 recn
      axicn mulcl mpan efcl 3syl absefi sylanbrc ) EBFGZHIZCJUIKJZUILIZMNZBOJZA
      PZLIZMNULAUIKCUNUINUOUKMUNUILQRDSUMBKJZUHKJZUJBTEKJUPUQUAEBUBUCUHUDUEBUFU
      G $.
      $( [25-Mar-2008] $)

    circgrp.2 $e |- T = ( x. |` ( C X. C ) ) $.
    $( The circle group ` T ` is an Abelian group.  (Contributed by Paul
       Chapman, 25-Mar-2008.) $)
    circgrp $p |- T e. Abel $=
      ( vx vy vz ci cr cv cmul co ce cfv wceq wrex cab wcel cc0 c2 cpi cico cc
      cabs c1 wa fveq2 eqeq1d elrab2 efifolem7 sylbi cle wbr clt w3a wb 0re
      2re pire remulcli elico2 mp2an 3simp1bi anim1i reximi2 syl visset eqeq1
      rexbidv elab sylibr eleq1 efielcirc syl5cbir r19.23aiv impbii eqriv
      axicn axresscn readdsubg efghgrpi ) FGIBCJHBGKZIFKZLMNOZPZFJQZGRZHKZBSZWI
      WHSZWJWIWEPZFJQZWKWJWLFTUAUBLMZUCMZQZWMWJWIUDSWIUEOZUFPZUGWPAKZUEOZUFPWRA
      WIUDBWSWIPWTWQUFWSWIUEUHUIDUJWIFUKULWLWLFWOJWDWOSZWDJSZWLXAXBTWDUMUNZWDWN
      UOUNZTJSWNJSXAXBXCXDUPUQURUAUBUSUTVATWNWDVBVCVDVEVFVGWGWMGWIHVHWCWIPWFWLF
      JWCWIWEVIVJVKZVLWKWMWJXEWLWJFJWLWJWEBSXBWIWEBVMAWDBDVNVOVPULVQVREVSVTWAWB
      $.
      $( [25-Mar-2008] $)
  $}

  ${
    $d A v x y z $.  $d A w x y $.  $d C v x y z $.  $d D x y $.  $d w v z $.
    shftefif1o.1 $e |- D = ( A [,) ( A + ( 2 x. pi ) ) ) $.
    shftefif1o.2 $e |- G = { <. x , y >. | ( x e. D /\
                             y = ( exp ` ( _i x. x ) ) ) } $.
    shftefif1o.3 $e |- C = { w e. CC | ( abs ` w ) = 1 } $.

    ${
      $d A u w $.  $d C u $.  $d D z $.  $d F w $.  $d G z $.  $d H z $.
      $d T u v x y $.
      shftefif1o.4 $e |- F = { <. x , y >. | ( x e. ( 0 [,) ( 2 x. pi ) ) /\
                               y = ( exp ` ( _i x. x ) ) ) } $.
      shftefif1o.5 $e |- S = { <. x , y >. |
              ( x e. ( A [,) ( A + ( 2 x. pi ) ) ) /\ y = ( x + -u A ) ) } $.
      shftefif1o.6 $e |- T = ( x. |` ( C X. C ) ) $.
      shftefif1o.7 $e |- L = { <. u , v >. | ( u e. C /\ v = { <. x , y >. |
                               ( x e. C /\ y = ( u T x ) ) } ) } $.
      shftefif1o.8 $e |- H = ( ( L ` ( exp ` ( _i x. A ) ) ) o. ( F o. S ) ) $.
      $( Lemma for ~ shftefif1o . $)
      shftefif1olem $p |- ( A e. RR -> G : D -1-1-onto-> C ) $=
        ( vz cr wcel wf1o ci cmul co ce cfv ccom wa f1oco wceq wb f1oeq1 ax-mp
        sylibr efielcirc cgr cabl circgrp ablgrp cdm cc cxp wss crn axmulopr
        fdmi cv cabs c1 crab ssrab2 eqsstri resgrprn mp3an grplactf1o mpan syl
        cc0 c2 cpi cico cneg caddc 2re pire remulcli readdcl mpan2 renegcl w3a
        icoshftf1o f1oeq2 mpd3an23 recn negid cmin negsub recnd sylanc recni
        pncan2 eqtrd opreq12d f1oeq3 mpbid efif1o wral opreq2 fveq2d fvex
        fvopab4 adantl wfun wf fvco3 3expa f1ofun f1of jca sylan fveq1i syl5eq
        mp3an1 eleq2i opreq1 oprex sylbi cle wbr clt elico2 mpdan syl5bb
        biimpa 3simp1d adantr wi icoshft syl5ib imp eleq12d grplactval cres
        oprvres opreqi resubcl pm3.26 3eqtrd efadd axicn mulcl adddi pncan3
        opreq2d eqtr3d 3eqtr2d eqtr4d r19.21aiva wfn f1ofn fnopab2 eqfnfv2
        biimprd mpd mpbird ) FUDUEZHGLUFZHGMUFZGGUGFUHUIZUJUKZNUKZUFZHGKIULZUFZ
        UVMUVKUVQUVSUMHGUVPUVRULZUFZUVMHGGUVPUVRUNMUVTUOUVMUWAUPUBHGMUVTUQURUSU
        VKUVOGUEZUVQCFGQUTZJVAUEZUWBUVQJVBUEUWDCGJQTVCJVDURZUVOEDNJGABUAUHVEVFV
        FVGZUOUWDGVFVHGJVIUOUWFVFUHVJVKUWEGCVLVMUKVNUOZCVFVOVFQUWGCVFVPVQUHJVFG
        TVRVSZVTWAWBZUVKHWCWDWEUHUIZWFUIZIUFZUVSUVKHFFWGZWHUIZFUWJWHUIZUWMWHUIZ
        WFUIZIUFZUWLUVKUWOUDUEZUWMUDUEZUWRUVKUWJUDUEUWSWDWEWIWJWKZFUWJWLWMZFWNZ
        UVKUWSUWTWOFUWOWFUIZUWQIUFZUWRABFUWOUWMISWPHUXDUOUWRUXEUPOHUXDUWQIWQURU
        SWRUVKUWQUWKUOZUWRUWLUPUVKUWNWCUWPUWJWFUVKFVFUEZUWNWCUOFWSZFWTWBUVKUWPU
        WOFXAUIZUWJUWOVFUEUXGUWPUXIUOUVKUWOFXBUVKUWOUXBXCUXHXDUVKUXGUXIUWJUOZUX
        HUXGUWJVFUEUXJUWJUXAXEFUWJXFWMWBXGXHZUWQUWKHIXIWBXJZUWKGKUFZUWLUVSABCGK
        RQXKZHUWKGKIUNWAWBZXDZUVKLMUOZUVLUVMUPUVKUCVLZLUKZUXRMUKZUOZUCHXLZUXQUV
        KUYAUCHUVKUXRHUEZUMZUXSUGUXRUHUIZUJUKZUXTUYCUXSUYFUOUVKABUXRUGAVLZUHUIZ
        UJUKZUYFHLUYGUXRUOUYHUYEUJUYGUXRUGUHXMXNPUYEUJXOXPXQUYDUXTUVOUGUXRFXAUI
        ZUHUIZUJUKZUHUIZUVNUYKWHUIZUJUKZUYFUYDUXTUXRIUKZKUKZUVPUKZUYLUVPUKZUYMU
        YDUXTUXRUVRUKZUVPUKZUYRUYDUXRUVTUKZVUAUXTUVPXRZHGUVRXSZUMUYCVUBVUAUOZUV
        KVUCVUDUYCVUEHGUXRUVPUVRXTYAUVKVUCVUDUVKUVQVUCUWIGGUVPYBWBUVKUVSVUDUXOH
        GUVRYCWBYDYEUXRMUVTUBYFYGUYDUYTUYQUVPHUWKIXSZUYCUYTUYQUOZUVKKXRZVUFUYCV
        UGUXMVUHUXNUWKGKYBURHUWKUXRKIXTYHUVKUWLVUFUXLHUWKIYCWBYEXNXGUYDUYQUYLUV
        PUYDUYQUYJKUKZUYLUYDUYPUYJKUYDUYPUXRUWMWHUIZUYJUYCUYPVUJUOZUVKUYCUXRUXD
        UEZVUKHUXDUXROYIZABUXRUYGUWMWHUIVUJUXDIUYGUXRUWMWHYJSUXRUWMWHYKXPYLXQUX
        RVFUEZUXGVUJUYJUOUYDUXRFXBUYDUXRUYDUXRUDUEZFUXRYMYNZUXRUWOYOYNZUVKUYCVU
        OVUPVUQWOZUVKVULVURUYCUVKUWSVULVURUPUXBFUWOUXRYPYQVUMYRYSYTZXCZUVKUXGUY
        CUXHUUAZXDZXGXNUYDUYJUWKUEZVUIUYLUOUYDVUJUWQUEZVVCUVKUYCVVDUVKVULVVDUYC
        UVKUWSUWTVULVVDUUBUXBUXCFUWOUWMUXRUUCWRVUMUUDUUEUYDVUJUYJUWQUWKVVBUVKUX
        FUYCUXKUUAUUFXJABUYJUYIUYLUWKKUYGUYJUOUYHUYKUJUYGUYJUGUHXMXNRUYKUJXOXPW
        BXGXNUWBUYLGUEZUYSUYMUOUYDUWBVVEUMZUYSUVOUYLJUIZUYMUWDUWBVVEUYSVVGUOUWE
        UVOUYLEDNJGABUAUWHUUGYHVVFUVOUYLUHGGVGUUHZUIUYMVVGUVOUYLGGUHUUIJVVHUVOU
        YLTUUJYGXGUVKUWBUYCUWCUUAUYDUYJUDUEZVVEVUOUVKVVIUYDUXRFUUKVUSUVKUYCUULX
        DZCUYJGQUTWBXDUUMUVNVFUEZUYKVFUEZUYOUYMUOUYDUVNUYKUUNUVKVVKUYCUVKUXGVVK
        UXHUGVFUEZUXGVVKUUOUGFUUPWAWBUUAUYDUYJVFUEZVVLUYDUYJVVJXCZVVMVVNVVLUUOU
        GUYJUUPWAWBXDUYDUYNUYEUJUYDUGFUYJWHUIZUHUIZUYNUYEUXGVVNVVQUYNUOZUYDVVMU
        XGVVNVVRUUOUGFUYJUUQYHVVAVVOXDUYDVVPUXRUGUHUXGVUNVVPUXRUOUYDFUXRUURVVAV
        UTXDUUSUUTXNUVAUVBUVCUVKUXQUYBUVKMHUVDZUXQUYBUPZUVKUVMVVSUXPHGMUVEWBLHU
        VDVVSVVTABHUYILUYHUJXOPUVFUCHLMUVGWAWBUVHUVIHGLMUQWBUVJ $.
        $( [25-Mar-2008] $)
    $}

    $( The exponential function of ` _i ` times a real number maps any
       closed-below, open-above real interval of length ` 2 x. pi ` one-to-one
       onto the unit circle.  (Contributed by Paul Chapman, 25-Mar-2008.) $)
    shftefif1o $p |- ( A e. RR -> G : D -1-1-onto-> C ) $=
      ( vz vv cv c2 cpi cmul co caddc cico wcel cneg wceq wa copab cxp cres
      cc0 ci ce cfv ccom eqid shftefif1olem ) ABCKLDEFAMZDDNOPQZRQSQTBMZUNDUARQ
      UBUCABUDZPEEUEUFZUNUGUOSQTUPUHUNPQUIUJUBUCABUDZGUHDPQUIUJLMZETKMUNETUPUTU
      NURQUBUCABUDUBUCLKUDZUJUSUQUKUKZVAHIJUSULUQULURULVAULVBULUM $.
      $( [25-Mar-2008] $)
  $}

  $( Lemma for ~ eff1i . $)
  eff1lem $p |- ( A e. CC -> ( exp ` A ) =
      ( ( abs ` ( exp ` A ) ) x. ( exp ` ( _i x. ( Im ` A ) ) ) ) ) $=
    ( cc wcel cre cfv ci cim cmul co caddc ce cabs wceq efadd recl recnd imcl
    axicn mulcl mpan syl sylanc replim fveq2d absef opreq1d 3eqtr4d ) ABCZADEZF
    AGEZHIZJIZKEZUIKEZUKKEZHIZAKEZUQLEZUOHIUIBCUKBCZUMUPMUHUIUKNUHUIAOPUHUJBCZU
    SUHUJAQPFBCUTUSRFUJSTUAUBUHAULKAUCUDUHURUNUOHAUEUFUG $.
    $( [16-Apr-2008] $)

  ${
    $d A v w z $.  $d D v w z $.
    eff1i.1 $e |- A e. RR $.
    eff1i.2 $e |- D = ( A [,) ( A + ( 2 x. pi ) ) ) $.
    eff1i.3 $e |- S = { v e. CC | ( Im ` v ) e. D } $.

    ${
      $d A a b $.  $d C a b w z $.  $d D a b $.  $d F a b v x $.  $d S x y $.
      $d a b v w x y z $.
      eff1i.4 $e |- F = { <. z , w >. | ( z e. D /\
                                          w = ( exp ` ( _i x. z ) ) ) } $.
      eff1i.5 $e |- C = { v e. CC | ( abs ` v ) = 1 } $.
      $( The exponential function maps the set ` S ` , of complex numbers with
         imaginary part in a closed-below, open-above real interval of length
         ` 2 x. pi ` starting at ` A ` , one-to-one to the nonzero complex
         numbers.  (Contributed by Paul Chapman, 16-Apr-2008.) $)
      eff1i $p |- ( exp |` S ) : S -1-1-> ( CC \ { 0 } ) $=
        ( vx vy cc cc0 csn cdif ce cres wf1 wf cv cfv wceq wi wral dff13 wss
        eff2 wcel cim fveq2 eleq1d elrab2 pm3.26bi ssriv fssres mp2an wa fvres
        eqeqan12d cre cabs absef cr wb reef11 recl syl2an bitrd syl5bi imp ci
        cmul co eff1lem biimpa opreq1d adantl eqeq2d wne imcl recnd axicn
        mulcl mpan syl efcl adantr abscl clt wbr gt0ne0 absgt0 efne0 sylanc
        mulcan syl112anc bitr3d mpbid ex opreq2 fveq2d fvex fvopab4 wf1o
        shftefif1o ax-mp f1of1 f1fveq pm3.27bi sylibd caddc replim cru jca
        biimprd mp2and sylbid rgen2a mpbir2an ) GPQRSZTGUAZUBGYDYEUCZNUDZYEUEZO
        UDZYEUEZUFZYGYIUFZUGZOGUHNGUHNOGYDYEUIPYDTUCGPUJYFUKNGPYGGULZYGPULZYGUM
        UEZFULZCUDZUMUEZFULZYQCYGPGYRYGUFYSYPFYRYGUMUNUOKUPZUQZURPYDGTUSUTYMNOG
        YNYIGULZVAZYKYGTUEZYITUEZUFZYLYNUUCYHUUEYJUUFYGGTVBYIGTVBVCUUDUUGYLUUDU
        UGVAYGVDUEZYIVDUEZUFZYPYIUMUEZUFZYLUUDUUGUUJYOYIPULZUUGUUJUGYNUUCYOUUMV
        AZUUEVEUEZUUFVEUEZUFZUUJUUGUUNUUQUUHTUEZUUITUEZUFZUUJYOUUMUUOUURUUPUUSY
        GVFYIVFVCUUHVGULZUUIVGULZUUTUUJVHYOUUMUUHUUIVIYGVJZYIVJZVKVLUUEUUFVEUNZ
        VMUUBUUCUUMUUKFULZYTUVFCYIPGYRYIUFYSUUKFYRYIUMUNUOKUPZUQZVKVNUUDUUGUULU
        UDUUGVOYPVPVQZTUEZVOUUKVPVQZTUEZUFZUULYOUUMUUGUVMUGYNUUCUUNUUGUVMUUNUUG
        VAZUUOUVJVPVQZUUPUVLVPVQZUFZUVMUUNUUGUVQYOUUMUUEUVOUUFUVPYGVRYIVRVCVSUV
        NUVOUUOUVLVPVQZUFZUVQUVMUVNUVRUVPUVOUUGUVRUVPUFUUNUUGUUOUUPUVLVPUVEVTWA
        WBUUNUVSUVMVHZUUGUUNUVJPULZUVLPULZUUOPULZUUOQWCZUVTYOUWAUUMYOUVIPULZUWA
        YOYPPULZUWEYOYPYGWDZWEVOPULZUWFUWEWFVOYPWGWHWIUVIWJWIWKUUMUWBYOUUMUVKPU
        LZUWBUUMUUKPULZUWIUUMUUKYIWDZWEUWHUWJUWIWFVOUUKWGWHWIUVKWJWIWAYOUWCUUMY
        OUUOYOUUEPULZUUOVGULZYGWJZUUEWLWIZWEWKYOUWDUUMUWMQUUOWMWNZUWDYOUUOWOUWO
        UWLUUEQWCZUWPYOUWLUWQUWPUUEWPVSUWNYGWQWRWRWKUVJUVLUUOWSWTWKXAXBXCUUBUVH
        VKYQUVFUVMUULVHYNUUCYQUVFVAZYPHUEZUUKHUEZUFZUVMUULYQUVFUWSUVJUWTUVLABYP
        VOAUDZVPVQZTUEZUVJFHUXBYPUFUXCUVITUXBYPVOVPXDXELUVITXFXGABUUKUXDUVLFHUX
        BUUKUFUXCUVKTUXBUUKVOVPXDXELUVKTXFXGVCFEHUBZUWRUXAUULVHFEHXHZUXEDVGULUX
        FIABCDEFHJLMXIXJFEHXKXJFEYPUUKHXLWHXAYNYOYQUUAXMUUCUUMUVFUVGXMVKXNVNUUD
        UUJUULVAZYLUGZUUGYOUUMUXHYNUUCUUNYLUXGUUNYLUUHUVIXOVQZUUIUVKXOVQZUFZUXG
        YOUUMYGUXIYIUXJYGXPYIXPVCUVAYPVGULZVAUVBUUKVGULZVAUXKUXGVHYOUUMUUHYPUUI
        UUKXQYOUVAUXLUVCUWGXRUUMUVBUXMUVDUWKXRVKVLXSUUBUVHVKWKXTXCYAYBYC $.
        $( [16-Apr-2008] $)

      $( The exponential function maps the set ` S ` , of complex numbers with
         imaginary part in a closed-below, open-above real interval of length
         ` 2 x. pi ` starting at ` A ` , onto the nonzero complex numbers.
         (Contributed by Paul Chapman, 16-Apr-2008.) $)
      effoi $p |- ( exp |` S ) : S -onto-> ( CC \ { 0 } ) $=
        ( vy vx va vb cc cc0 csn cdif ce cres wfo wf cv cfv wceq wrex wral
        dffo3 wss eff2 wcel cim fveq2 eleq1d elrab2 pm3.26bi ssriv fssres
        mp2an cabs cr ccnv ci cdiv co cmul caddc eqeq2d rcla4ev addcl crp clt
        wbr elrp eldifi abscl syl wne wa eldifsn absgt0 biimpa sylbi sylanbrc
        wf1o reeff1o2 f1ocnv ax-mp f1of ffvelrni recnd c1 eqeq1d divcl gt0ne0
        sylanc syl3anc absdiv absidm opreq2d divid 3eqtrd shftefif1o cle c2
        cpi cico w3a eleq2i wb 2re pire remulcli readdcli elico2 bitri
        3simp1bi 3syl axicn mulcl mpan crim eqeltrd fvres efadd f1ocnvfv2
        eqtr3d opreq2 fveq2d copab eleq1 adantr id eqeqan12rd anbi12d cbvopabv
        eqtri fvex fvopab4 opreq12d divcan2 eqtr2d rgen mpbir2an ) GRSTZUAZUBGU
        CZUDGUUIUUJUEZNUFZOUFZUUJUGZUHZOGUIZNUUIUJONGUUIUUJUKRUUIUBUEGRULUUKUMO
        GRUUMGUNUUMRUNUUMUOUGZFUNZCUFZUOUGZFUNZUURCUUMRGUUSUUMUHUUTUUQFUUSUUMUO
        UPUQKURUSUTRUUIGUBVAVBUUPNUUIUULVCUGZUBVDUCZVEZUGZVFUULUVBVGVHZHVEZUGZV
        IVHZVJVHZGUNZUULUVJUUJUGZUHZUUPUULUUIUNZUUOUVMOUVJGUUMUVJUHUUNUVLUULUUM
        UVJUUJUPVKVLUVKUVJRUNZUVJUOUGZFUNZUVNUVAUVQCUVJRGUUSUVJUHUUTUVPFUUSUVJU
        OUPUQKURUVERUNZUVIRUNZUVOUVNUVEUVIVMUVNUVEUVNUVBVNUNZUVEVDUNZUVTUVBVDUN
        ZSUVBVOVPZUVNUVBVQUVNUULRUNZUWBUULRUUHVRZUULVSVTZUVNUWDUULSWAZWBUWCUULR
        SWCUWDUWGUWCUULWDWEWFZWGZVNVDUVBUVDVNVDUVDWHZVNVDUVDUEVDVNUVCWHZUWJWIVD
        VNUVCWJWKVNVDUVDWLWKWMVTZWNZUVNUVHRUNZUVSUVNUVHUVNUVFEUNZUVHFUNZUVHVDUN
        ZUWOUVFRUNZUVFVCUGZWOUHZUVNUUSVCUGZWOUHUWTCUVFREUUSUVFUHUXAUWSWOUUSUVFV
        CUPWPMURUWDUVBRUNZUVBSWAZUWRUVNUULUVBWQUWEUVNUVBUWFWNZUWBUWCUXCUVNUVBWR
        UWFUWHWSZWTUVNUWSUVBUVBVCUGZVGVHZUVBUVBVGVHZWOUWDUXBUXCUWSUXGUHUVNUULUV
        BXAUWEUXDUXEWTUVNUWDUXGUXHUHUWEUWDUXFUVBUVBVGUULXBXCVTUXBUXCUXHWOUHUVNU
        VBXDUXDUXEWSXEWGZEFUVFUVGEFUVGWHZEFUVGUEFEHWHZUXJDVDUNZUXKIABCDEFHJLMXF
        WKZFEHWJWKEFUVGWLWKWMZUWPUWQDUVHXGVPZUVHDXHXIVIVHZVJVHZVOVPZUWPUVHDUXQX
        JVHZUNZUWQUXOUXRXKZFUXSUVHJXLUXLUXQVDUNUXTUYAXMIDUXPIXHXIXNXOXPXQDUXQUV
        HXRVBXSXTYAZWNVFRUNUWNUVSYBVFUVHYCYDVTZWSUVNUVPUVHFUWAUWQUVPUVHUHUVNUVE
        UVHYEUWLUYBWSUVNUWOUWPUXIUXNVTYFWGZUVNUVLUVJUBUGZUULUVNUVKUVLUYEUHUYDUV
        JGUBYGVTUVNUYEUVEUBUGZUVIUBUGZVIVHZUVBUVFVIVHZUULUVRUVSUYEUYHUHUVNUVEUV
        IYHUWMUYCWSUVNUYFUVBUYGUVFVIUVNUVEUVCUGZUYFUVBUVNUWAUYJUYFUHUWLUVEVDUBY
        GVTUVNUVTUYJUVBUHZUWIUWKUVTUYKWIVDVNUVBUVCYIYDVTYJUVNUVHHUGZUYGUVFUVNUW
        OUWPUYLUYGUHUXIUXNPQUVHVFPUFZVIVHZUBUGZUYGFHUYMUVHUHUYNUVIUBUYMUVHVFVIY
        KYLHAUFZFUNZBUFZVFUYPVIVHZUBUGZUHZWBZABYMUYMFUNZQUFZUYOUHZWBZPQYMLVUBVU
        FABPQUYPUYMUHZUYRVUDUHZWBUYQVUCVUAVUEVUGUYQVUCXMVUHUYPUYMFYNYOVUHVUGUYR
        VUDUYTUYOVUHYPVUGUYSUYNUBUYPUYMVFVIYKYLYQYRYSYTUVIUBUUAUUBYAUVNUWOUYLUV
        FUHZUXIUXKUWOVUIUXMFEUVFHYIYDVTYJUUCUWDUXBUXCUYIUULUHUVNUULUVBUUDUWEUXD
        UXEWTXEUUEWSUUFUUG $.
        $( [16-Apr-2008] $)
    $}

    $( The exponential function maps the set ` S ` , of complex numbers with
       imaginary part in a closed-below, open-above real interval of length
       ` 2 x. pi ` starting at ` A ` , one-to-one onto the nonzero complex
       numbers. ` A ` would normally be fixed at ` 0 ` or ` -u pi ` , according
       to choice of principal domain for the exponential function.
       (Contributed by Paul Chapman, 16-Apr-2008.) $)
    eff1oi $p |- ( exp |` S ) : S -1-1-onto-> ( CC \ { 0 } ) $=
      ( vz vw cc cc0 csn cdif ce cres wf1o wf1 wfo df-f1o cv cabs cfv c1 wceq
      crab wcel ci cmul co wa copab eqid eff1i effoi mpbir2an ) DJKLMZNDOZPDUPU
      QQDUPUQRDUPUQSHIABATUAUBUCUDAJUEZCDHTZCUFITUGUSUHUINUBUDUJHIUKZEFGUTULZUR
      ULZUMHIABURCDUTEFGVAVBUNUO $.
      $( [16-Apr-2008] $)
  $}

  $( The exponential function is periodic.  (Contributed by Paul Chapman,
     21-Apr-2008.) $)
  efper $p |- ( ( A e. CC /\ K e. ZZ ) ->
           ( exp ` ( A + ( ( _i x. ( 2 x. pi ) ) x. K ) ) ) = ( exp ` A ) ) $=
    ( cc wcel cz wa ci c2 cpi cmul co caddc ce cfv wceq efadd zcn axicn 2re
    pire remulcli recni mulcli mulcl mpan syl sylan2 c1 ccos csin mpan2 efival
    3syl mul12 mp3an13 mulcom eqtrd fveq2d cc0 cos2kpi sin2kpi opreq2d mul01i
    syl6eq opreq12d ax1cn addid1i 3eqtr3d efcl mulid1 sylan9eqr ) ACDZBEDZFAGHI
    JKZJKZBJKZLKMNZAMNZVPMNZJKZVRVLVPCDZVQVTOVMAVPPVMBCDZWABQZVOCDZWBWAGVNRVNHI
    STUAUBZUCZVOBUDUEUFUGVMVLVTVRUHJKZVRVMVSUHVRJVMGBVNJKZJKZMNZWHUINZGWHUJNZJK
    ZLKZVSUHVMWBWHCDZWJWNOWCWBVNCDZWOWEBVNUDUKWHULUMVMWIVPMVMWBWIVPOWCWBWIBVOJK
    ZVPGCDWBWPWIWQORWEGBVNUNUOWBWDWQVPOWFBVOUPUKUQUFURVMWNUHUSLKUHVMWKUHWMUSLBU
    TVMWMGUSJKUSVMWLUSGJBVAVBGRVCVDVEUHVFVGVDVHVBVLVRCDWGVROAVIVRVJUFVKUQ $.
    $( [21-Apr-2008] $)

  $( The exponential function restricted to its principal domain maps
     one-to-one onto the nonzero complex numbers.  (Contributed by Paul
     Chapman, 21-Apr-2008.) $)
  eff1o $p |- ( exp |` { x e. CC | ( Im ` x ) e. ( -u pi [,) pi ) } ) :
    { x e. CC | ( Im ` x ) e. ( -u pi [,) pi ) } -1-1-onto-> ( CC \ { 0 } ) $=
    ( cpi cneg c2 cmul co caddc cico cv cim cfv wcel cc crab pire renegcli
    eqid wb cmin 2cn recni mulcli negsubi addcomi 2timesi eqcomi subaddrii
    3eqtr3i opreq2i eleq2i a1i rabbiia eff1oi ) ABCZUNUNDBEFZGFZHFZAIZJKZUNBHFZ
    LZAMNZBOPZUQQUSUQLZAMNVBVDVAAMVDVARURMLUQUTUSUPBUNHUOUNGFUOBSFUPBUOBDBTBOUA
    ZUBZVEUCUOUNVFUNVCUAUDUOBBVFVEVEUOBBGFBVEUEUFUGUHUIUJUKULUFUM $.
    $( [21-Apr-2008] $)

