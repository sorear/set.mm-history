$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Mapping of the exponential function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.  $d B x $.  $d C x $.
    efgh.1 $e |- F = ( x e. CC |-> ( exp ` ( A x. x ) ) ) $.
    $( The exponential function of a scaled complex number is a group
       homomorphism from the group of complex numbers under addition to the set
       of complex numbers under multiplication.  (Contributed by Paul Chapman,
       25-Apr-2008.)  (Revised by Mario Carneiro, 11-May-2014.) $)
    efgh $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( F ` ( B + C ) ) = ( ( F ` B ) x. ( F ` C ) ) ) $=
      ( cc wcel caddc co cmul ce cfv fveq2d wceq mulcl 3adant1 oveq2 fvex fvmpt
      w3a adddi 3adant3 3adant2 efadd syl2anc eqtrd addcl syl oveqan12d 3eqtr4d
      cv ) BGHZCGHZDGHZUAZBCDIJZKJZLMZBCKJZLMZBDKJZLMZKJZUQEMZCEMZDEMZKJZUPUSUT
      VBIJZLMZVDUPURVILBCDUBNUPUTGHZVBGHZVJVDOUMUNVKUOBCPUCUMUOVLUNBDPUDUTVBUEU
      FUGUPUQGHZVEUSOUNUOVMUMCDUHQAUQBAULZKJZLMZUSGEVNUQOVOURLVNUQBKRNFURLSTUIU
      NUOVHVDOUMUNUOVFVAVGVCKACVPVAGEVNCOVOUTLVNCBKRNFUTLSTADVPVCGEVNDOVOVBLVND
      BKRNFVBLSTUJQUK $.
      $( [11-May-2014] $) $( [25-Apr-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z ph $.  $d x y z X $.
    efghgrp.1 $e |- S = { y | E. x e. X y = ( exp ` ( A x. x ) ) } $.
    efghgrp.2 $e |- G = ( x. |` ( S X. S ) ) $.
    efghgrp.3 $e |- ( ph -> A e. CC ) $.
    efghgrp.4 $e |- ( ph -> X C_ CC ) $.
    efghgrp.5 $e |- ( + |` ( X X. X ) ) e. ( SubGrpOp ` + ) $.
    $( The image of a subgroup of the group ` + ` , under the exponential
       function of a scaled complex number, is an Abelian group.  (Contributed
       by Paul Chapman, 25-Apr-2008.)  (Revised by Mario Carneiro,
       12-May-2014.) $)
    efghgrp $p |- ( ph -> G e. AbelOp ) $=
      ( cmul cc cfv caddc wceq eqid wcel a1i vz cv co ce cmpt cxp cres crn cima
      cabl wrex cab rnmpt eqtr4i df-ima wss resmpt syl syl5eq cdm cgr ax-addopr
      rneqd fdmi csubg cnaddablo subgabl mp2an ablogrpo resgrprn syl3anc eqtr3d
      imaeq2d xpeq12d reseq2d ax-mp grporn mulcl efcl sylan fmptd wfn ax-mulopr
      wa ssid wf ffn efgh 3expb ghsubablo eqeltrd ) AFMBNDBUBZMUCZUDOZUEZPGGUFU
      GZUHZUIZWRUFZUGZUJAFMEEUFZUGWTIAXAWSMAEWREWRAEBGWNUEZUHZWRECUBZWNQBGUKCUL
      XCHBCGWNXBXBRUMUNAWOGUIZXCWRAXEWOGUGZUHXCWOGUOAXFXBAGNUPZXFXBQKBNGWNUQURV
      CUSAGWQWOAPUTNNUFZQZWPVASZXGGWQQXIAXHNPVBVDZTAWPUJSZXJXLAPUJSZWPPVEOSZXLV
      FLPWPVGVHTZWPVIURKPWPNGWPRVJVKVMVLUSZXPVNVOUSACUANWPWOPWTMWRNNWQXNALTPNXM
      PVASVFPVIVPXKVQABNWNNWOADNSZWLNSZWNNSZJXQXRWDWMNSXSDWLVRWMVSURVTWORZWANNU
      PANWETMXHWBZAXHNMWFYAWCXHNMWGVPTAXQXDNSZUAUBZNSZWDXDYCPUCWOOXDWOOYCWOOMUC
      QZJXQYBYDYEBDXDYCWOXTWHWIVTWQRWRRWTRXOWJWK $.
      $( [12-May-2014] $) $( [25-Apr-2008] $)
  $}

  ${
    $d y z $.  $d y A $.  $d y D $.
    efif1olem1.1 $e |- D = ( A (,] ( A + ( 2 x. pi ) ) ) $.
    $( Lemma for ~ efif1o . $)
    efif1olem1 $p |- ( ( A e. RR /\ ( x e. D /\ y e. D ) ) ->
                         ( abs ` ( x - y ) ) < ( 2 x. pi ) ) $=
      ( cr wcel cv wa cmin co c2 clt wbr caddc wb readdcl sylancl mpbid syl3anc
      cabs cfv cpi cmul cle cioc w3a simprl syl6eleq cxr rexr adantr simpl pire
      2re remulcli elioc2 syl2anc simp1d simprr absdiflt simp3d simp2d ltsubadd
      a1i ltadd1 lelttrd mpbird mpbir2and ) CFGZAHZDGZBHZDGZIZIZVKVMJKUAUBLUCUD
      KZMNZVMVQJKVKMNZVKVMVQOKZMNZVPVKFGZVMFGZVQFGZVRVSWAIPVPWBCVKMNZVKCVQOKZUE
      NZVPVKCWFUFKZGZWBWEWGUGZVPVKDWHVJVLVNUHEUIVPCUJGZWFFGZWIWJPVJWKVOCUKULZVP
      VJWDWLVJVOUMZLUCUOUNUPZCVQQRZCWFVKUQURSZUSZVPWCCVMMNZVMWFUENZVPVMWHGZWCWS
      WTUGZVPVMDWHVJVLVNUTEUIVPWKWLXAXBPWMWPCWFVMUQURSZUSZWDVPWOVEZVKVMVQVATVPV
      SVMVKVQOKZMNZVPVMWFXFXDWPVPWBWDXFFGWRWOVKVQQRVPWCWSWTXCVBVPWEWFXFMNZVPWBW
      EWGWQVCVPVJWBWDWEXHPWNWRXECVKVQVFTSVGVPWCWDWBVSXGPXDXEWRVMVQVKVDTVHVPVKWF
      VTWRWPVPWCWDVTFGXDWOVMVQQRVPWBWEWGWQVBVPWSWFVTMNZVPWCWSWTXCVCVPVJWCWDWSXI
      PWNXDXECVMVQVFTSVGVI $.
      $( [13-May-2014] $)

    $( Lemma for ~ efif1o . $)
    efif1olem2 $p |- ( ( A e. RR /\ z e. RR ) ->
                       E. y e. D ( ( z - y ) / ( 2 x. pi ) ) e. ZZ ) $=
      ( cr wcel cmul co caddc cmin cdiv cz wbr sylancl syl2anc syl3anc cc recnd
      wceq cv wa c2 cpi cmo wrex cioc clt cle cxr w3a wb rexr adantr simpl pire
      2re remulcli readdcl elioc2 crp resubcl pipos mulgt0ii elrpii modcl modlt
      2pos a1i ltadd2 mpbid ltaddsub modge0 subge02 mpbir3and syl6eleqr c1 cneg
      cc0 cfl cfv modval oveq2d wne gt0ne0ii redivcl mp3an23 syl remulcl subsub
      flcl zre sylancr recni simpr pnncan oveq1d 3eqtrd addcl subsub4 negsubdi2
      pncan negeqd eqtr3d 1z znegcl ax-mp zcn mulcomi mulm1i eqtri subdi eqtr4d
      syl6eqr 3eqtr2d zsubcl divcan3 eqtrd eqeltrd oveq2 eleq1d rcla4ev ) CFGZB
      UAZFGZUBZCUCUDHIZJIZCYDKIZYGUEIZKIZDGYDYKKIZYGLIZMGZYDAUAZKIZYGLIZMGZADUF
      YFYKCYHUGIZDYFYKYSGZYKFGZCYKUHNZYKYHUINZYFCUJGZYHFGZYTUUAUUBUUCUKULYCUUDY
      ECUMUNYFYCYGFGZUUEYCYEUOZUCUDUQUPURZCYGUSOZCYHYKUTPYFUUEYJFGZUUAUUIYFYIFG
      ZYGVAGZUUJCYDVBZYGUUHUCUDUQUPVHVCVDZVEZYIYGVFOZYHYJVBPYFCYJJIYHUHNZUUBYFY
      JYGUHNZUUQYFUUKUULUURUUMUUOYIYGVGOYFUUJUUFYCUURUUQULUUPUUFYFUUHVIUUGYJYGC
      VJQVKYFYCUUJUUEUUQUUBULUUGUUPUUICYJYHVLQVKYFVSYJUINZUUCYFUUKUULUUSUUMUUOY
      IYGVMOYFUUEUUJUUSUUCULUUIUUPYHYJVNPVKVOEVPYFYMVQVRZYIYGLIZVTWAZKIZMYFYMYG
      UVCHIZYGLIZUVCYFYLUVDYGLYFYLYDYGYDJIZYGUVBHIZJIZKIZYDUVFKIZUVGKIZUVDYFYKU
      VHYDKYFYKYHYIUVGKIZKIZYHYIKIZUVGJIZUVHYFYJUVLYHKYFUUKUULYJUVLTUUMUUOYIYGW
      BOWCYFYHRGYIRGUVGRGZUVMUVOTYFYHUUISYFYIUUMSYFUVGYFUUFUVBFGZUVGFGUUHYFUVBM
      GZUVQYFUVAFGZUVRYFUUKUVSUUMUUKUUFYGVSWDZUVSUUHYGUUHUUNWEZYIYGWFWGWHUVAWKW
      HZUVBWLWHZYGUVBWIWMSZYHYIUVGWJQYFUVNUVFUVGJYFCRGYGRGZYDRGZUVNUVFTYFCUUGSU
      WEYFYGUUHWNZVIZYFYDYCYEWOSZCYGYDWPQWQWRWCYFUWFUVFRGZUVPUVKUVITUWIYFUWEUWF
      UWJUWGUWIYGYDWSWMZUWDYDUVFUVGWTQYFUVKYGUUTHIZUVGKIZUVDYFUVJUWLUVGKYFUVJYG
      VRZUWLYFUVFYDKIZVRZUVJUWNYFUWJUWFUWPUVJTUWKUWIUVFYDXAPYFUWOYGYFUWEUWFUWOY
      GTUWHUWIYGYDXBPXCXDUWLUUTYGHIUWNYGUUTUWGUUTMGZUUTRGZVQMGUWQXEVQXFXGZUUTXH
      XGZXIYGUWGXJXKXNWQYFUWEUWRUVBRGUVDUWMTUWHUWRYFUWTVIYFUVBUWCSYGUUTUVBXLQXM
      XOWQYFUVCRGZUVEUVCTZYFUVCMGZUXAYFUWQUVRUXCUWSUWBUUTUVBXPWMZUVCXHWHUXAUWEU
      VTUXBUWGUWAUVCYGXQWGWHXRUXDXSYRYNAYKDYOYKTZYQYMMUXEYPYLYGLYOYKYDKXTWQYAYB
      P $.
      $( [13-May-2014] $)
  $}

  ${
    $d w x y z A $.  $d w x y C $.  $d x y F $.  $d w x y z ph $.  $d y z S $.
    $d w x y z D $.
    efif1o.1 $e |- F = ( w e. D |-> ( exp ` ( _i x. w ) ) ) $.
    efif1o.2 $e |- C = ( `' abs " { 1 } ) $.
    ${
      efif1olem3.3 $e |- ( ph -> D C_ RR ) $.
      efif1olem3.4 $e |- ( ( ph /\ ( x e. D /\ y e. D ) ) ->
                           ( abs ` ( x - y ) ) < ( 2 x. pi ) ) $.
      efif1olem3.5 $e |- ( ( ph /\ z e. RR ) ->
                           E. y e. D ( ( z - y ) / ( 2 x. pi ) ) e. ZZ ) $.
      efif1olem3.6 $e |- S = ( sin |` ( -u ( pi / 2 ) [,] ( pi / 2 ) ) ) $.
      $( The exponential function of an imaginary number maps any interval of
         length ` 2 pi ` one-to-one onto the unit circle.  (Contributed by Paul
         Chapman, 16-Mar-2008.)  (The proof was shortened by Mario Carneiro,
         13-May-2014.) $)
      efif1olem3 $p |- ( ph -> F : D -1-1-onto-> C ) $=
        ( wceq co wcel cc syl wf1 wfo wf1o wf cv wi wral ci cmul ce wa cr sseld
        cfv imp cabs ccnv c1 csn cima ax-icn recn mulcl sylancr efcl absefi wfn
        wb absf ffn ax-mp ax-1cn fniniseg mp2an sylanbrc syl6eleqr fmptd cc0 c2
        cmin cpi cdiv clt wbr wss ad2antrr simplrl sseldd recnd simplrr syl2anc
        subcl wne 2re remulcli recni mp3an23 cle 0re ltleii adantr abscl pm3.2i
        pire 1re cn0 cz divcan5 a1i subdi syl3anc fveq2d simpr oveq2 fvex fvmpt
        3eqtr3d oveq1d eqtrd 3eqtrd efeq1 mpbid eqeltrrd wrex cneg cicc sylancl
        simpld cexp simprd sq11 renegcli csin 3syl ralrimiva oveq1 eleq1d caddc
        oveq12d sqcl pipos mulgt0ii gt0ne0ii absdiv absid oveq2i syl6eq mulid1i
        2pos syl6breqr ltdivmul mpbird ine0 efsub efne0 divid nn0abscl nn0lt10b
        eqbrtrd divcl abs00 diveq0 subeq0 ex ralrimivva dff13 csqr cim syl6eleq
        sylib sqrcl imcl absimle 2nn0 absexp sq1 syl6eqr absge0 mpanr12 breqtrd
        sqrth lt01 absle w3a elicc2 syl3anbrc cres resinf1o f1oeq1 mpbir f1ocnv
        f1of ffvelrni rehalfcl iccssre sseli remulcl rexbidv rcla4v npcan efadd
        mp2b sylc 2cn mul12 mp3an12 2z efexp ccos cre sincossq absvalsq2 fveq1i
        3eqtr2d fvres syl5eq f1ocnvfv2 eqtr3d sincl coscl pncan2 recl cosq14ge0
        pncan recoscl oveq2d efival replim 3eqtr4d mulid2 eqeq12d syl5ib bitr2d
        sqrrege0 syl22anc adantl eqeq2d 3imtr4d reximdva mpd dffo3 df-f1o ) AGF
        IUAZGFIUBZGFIUCAGFIUDZBUEZIUNZCUEZIUNZPZVUFVUHPZUFZCGUGBGUGVUCAEGUHEUEZ
        UIQZUJUNZFIAVUMGRZUKVUMULRZVUOFRAVUPVUQAGULVUMLUMUOVUQVUOUPUQURUSUTZFVU
        QVUOSRZVUOUPUNURPZVUOVURRZVUQVUNSRZVUSVUQUHSRZVUMSRVVBVAVUMVBUHVUMVCVDV
        UNVETVUMVFUPSVGZURSRZVVAVUSVUTUKVHSULUPUDVVDVISULUPVJVKZVLSURVUOUPSVMVN
        VOKVPTJVQZAVULBCGGAVUFGRZVUHGRZUKZUKZVUJVUKVVKVUJUKZVUFVUHVTQZVRPZVUKVV
        LVVMVSWAUIQZWBQZVRPZVVNVVLVVPUPUNZVRPZVVQVVLVVRURWCWDZVVSVVLVVRVVMUPUNZ
        VVOWBQZURWCVVLVVRVWAVVOUPUNZWBQZVWBVVLVVMSRZVVRVWDPZVVLVUFSRZVUHSRZVWEV
        VLVUFVVLGULVUFAGULWEZVVJVUJLWFZAVVHVVIVUJWGZWHWIZVVLVUHVVLGULVUHVWJAVVH
        VVIVUJWJZWHWIZVUFVUHWLWKZVWEVVOSRZVVOVRWMZVWFVVOVSWAWNXDWOZWPZVVOVWRVSW
        AWNXDUUIUUAUUBZUUCZVVMVVOUUDWQTVWCVVOVWAWBVVOULRZVRVVOWRWDVWCVVOPVWRVRV
        VOWSVWRVWTWTVVOUUEVNUUFUUGVVLVWBURWCWDZVWAVVOURUIQZWCWDZVVLVWAVVOVXDWCV
        VKVWAVVOWCWDVUJMXAVVOVWSUUHUUJVVLVWAULRZVXCVXEVHZVVLVWEVXFVWOVVMXBTVXFU
        RULRZVXBVRVVOWCWDZUKVXGXEVXBVXIVWRVWTXCVWAURVVOUUKWQTUULUUSVVLVVRXFRZVV
        TVVSVHVVLVVPXGRVXJVVLUHVVMUIQZUHVVOUIQZWBQZVVPXGVVLVWEVXMVVPPZVWOVWEVWP
        VWQUKZVVCUHVRWMZUKZVXNVWPVWQVWSVXAXCZVVCVXPVAUUMXCZVVMVVOUHXHWQTVVLVXKU
        JUNZURPZVXMXGRZVVLVXTUHVUFUIQZUHVUHUIQZVTQZUJUNZVYCUJUNZVYDUJUNZWBQZURV
        VLVXKVYEUJVVLVVCVWGVWHVXKVYEPVVCVVLVAXIVWLVWNUHVUFVUHXJXKXLVVLVYCSRZVYD
        SRZVYFVYIPVVLVVCVWGVYJVAVWLUHVUFVCVDVVLVVCVWHVYKVAVWNUHVUHVCZVDZVYCVYDU
        UNWKVVLVYIVYHVYHWBQZURVVLVYGVYHVYHWBVVLVUGVUIVYGVYHVVKVUJXMVVLVVHVUGVYG
        PVWKEVUFVUOVYGGIVUMVUFPVUNVYCUJVUMVUFUHUIXNXLJVYCUJXOXPTVVLVVIVUIVYHPZV
        WMEVUHVUOVYHGIVUMVUHPVUNVYDUJVUMVUHUHUIXNXLJVYDUJXOXPZTXQXRVVLVYHSRZVYH
        VRWMZVYNURPVVLVYKVYQVYMVYDVEZTVVLVYKVYRVYMVYDUUOTVYHUUPWKXSXTVVLVXKSRZV
        YAVYBVHVVLVVCVWEVYTVAVWOUHVVMVCVDVXKYATYBYCVVPUUQTVVRUURTYBVVLVVPSRZVVS
        VVQVHVVLVWEWUAVWOVWEVWPVWQWUAVWSVXAVVMVVOUUTWQTVVPUVATYBVVLVWEVVQVVNVHZ
        VWOVWEVWPVWQWUBVWSVXAVVMVVOUVBWQTYBVVLVWGVWHVVNVUKVHVWLVWNVUFVUHUVCWKYB
        UVDUVEBCGFIUVFVOAVUEVUFVUIPZCGYDZBFUGVUDVVGAWUDBFAVUFFRZUKZVSVUFUVGUNZU
        VHUNZHUQZUNZUIQZVUHVTQZVVOWBQZXGRZCGYDZWUDWUFWUKULRZDUEZVUHVTQZVVOWBQZX
        GRZCGYDZDULUGZWUOWUFVSULRWUJULRZWUPWNWUFWUHURYEZURYFQZRZWUJWAVSWBQZYEZW
        VGYFQZRZWVCWUFWUHULRZWVDWUHWRWDZWUHURWRWDZWVFWUFWUGSRZWVKWUFVWGWVNWUFVW
        GVUFUPUNZURPZWUFVUFVURRZVWGWVPUKZWUFVUFFVURAWUEXMKUVIVVDVVEWVQWVRVHVVFV
        LSURVUFUPSVMVNUVJZYHZVUFUVKTZWUGUVLTZWUFWVLWVMWUFWUHUPUNZURWRWDZWVLWVMU
        KZWUFWWCWUGUPUNZURWRWUFWVNWWCWWFWRWDWWAWUGUVMTWUFWWFVSYIQZURVSYIQZPZWWF
        URPZWUFWWGURWWHWUFWUGVSYIQZUPUNZWVOWWGURWUFWWKVUFUPWUFVWGWWKVUFPWVTVUFU
        WATZXLWUFWVNVSXFRWWLWWGPWWAUVNWUGVSUVOYGWUFVWGWVPWVSYJXQZUVPUVQWUFWWFUL
        RZVRWWFWRWDZWWIWWJVHZWUFWVNWWOWWAWUGXBTWUFWVNWWPWWAWUGUVRTWWOWWPUKVXHVR
        URWRWDWWQXEVRURWSXEUWBWTWWFURYKUVSWKYBUVTWUFWVKVXHWWDWWEVHWWBXEWUHURUWC
        YGYBZYHWUFWVLWVMWWRYJWVDULRVXHWVFWVKWVLWVMUWDVHURXEYLXEWVDURWUHUWEVNUWF
        ZWVEWVIWUHWUIWVIWVEHUCZWVEWVIWUIUCWVEWVIWUIUDWWTWVIWVEYMWVIUWGZUCZUWHHW
        XAPWWTWXBVHOWVIWVEHWXAUWIVKUWJZWVIWVEHUWKWVEWVIWUIUWLUXBUWMZWVIULWUJWVH
        ULRWVGULRZWVIULWEWVGWAULRWXEXDWAUWNVKZYLWXFWVHWVGUWOVNUWPYNZVSWUJUWQVDZ
        AWVBWUEAWVADULNYOXAWVAWUODWUKULWUQWUKPZWUTWUNCGWXIWUSWUMXGWXIWURWULVVOW
        BWUQWUKVUHVTYPXRYQUWRUWSUXCWUFWUNWUCCGWUFVVIUKZUHWULUIQZUJUNZURPZVUFVYH
        PZWUNWUCWXMWXLVYHUIQZURVYHUIQZPWXJWXNWXLURVYHUIYPWXJWXOVUFWXPVYHWXJWXKV
        YDYRQZUJUNZUHWUKUIQZUJUNZWXOVUFWXJWXQWXSUJWXJWXQWXSVYDVTQZVYDYRQZWXSWXJ
        WXKWYAVYDYRWXJVVCWUKSRZVWHWXKWYAPVVCWXJVAXIWXJWUKWUFWUPVVIWXHXAWIZWXJVU
        HWXJGULVUHAVWIWUEVVILWFWUFVVIXMWHWIZUHWUKVUHXJXKXRWXJWXSSRZVYKWYBWXSPWX
        JVVCWYCWYFVAWYDUHWUKVCVDWXJVVCVWHVYKVAWYEVYLVDZWXSVYDUWTWKXSXLWXJWXKSRZ
        VYKWXRWXOPWXJVVCWULSRZWYHVAWXJWYCVWHWYIWYDWYEWUKVUHWLWKZUHWULVCVDZWYGWX
        KVYDUXAWKWUFWXTVUFPVVIWUFWXTUHWUJUIQZUJUNZVSYIQZWWKVUFWUFWXTVSWYLUIQZUJ
        UNZWYNWUFWXSWYOUJWUFWUJSRZWXSWYOPZWUFWUJWXGWIZVVCVSSRWYQWYRVAUXDUHVSWUJ
        UXEUXFTXLWUFWYLSRZVSXGRWYPWYNPWUFVVCWYQWYTVAWYSUHWUJVCVDUXGWYLVSUXHYGXS
        WUFWYMWUGVSYIWUFWUJUXIUNZUHWUJYMUNZUIQZYRQZWUGUXJUNZUHWUHUIQZYRQZWYMWUG
        WUFXUAXUEXUCXUFYRWUFXUAVSYIQZXUEVSYIQZPZXUAXUEPZWUFXUBVSYIQZXUHYRQZXULV
        TQZXUIWUHVSYIQZYRQZXUOVTQZXUHXUIWUFXUMXUPXULXUOVTWUFXUMURWWGXUPWUFWYQXU
        MURPWYSWUJUXKTWWNWUFWVNWWGXUPPWWAWUGUXLTUXNWUFXUBWUHVSYIWUFWUJHUNZXUBWU
        HWUFXURWUJWXAUNZXUBWUJHWXAOUXMWUFWVJXUSXUBPWUFWVFWVJWWSWXDTZWUJWVIYMUXO
        TUXPWUFWWTWVFXURWUHPWXCWWSWVIWVEWUHHUXQVDUXRZXRZYSWUFXULSRZXUHSRZXUNXUH
        PWUFWYQXUBSRXVCWYSWUJUXSXUBYTYNZWUFWYQXUASRXVDWYSWUJUXTXUAYTYNXULXUHUYA
        WKWUFXUISRZXUOSRXUQXUIPWUFXUESRXVFWUFXUEWUFWVNXUEULRZWWAWUGUYBTZWIXUEYT
        TWUFXULXUOSXVBXVEYCXUIXUOUYDWKXQWUFXUAULRZVRXUAWRWDZXVGVRXUEWRWDZXUJXUK
        VHWUFWVCXVIWXGWUJUYETWUFWVJXVJXUTWUJUYCTXVHWUFVWGXVKWVTVUFUYNTXUAXUEYKU
        YOYBWUFXUBWUHUHUIXVAUYFYSWUFWYQWYMXUDPWYSWUJUYGTWUFWVNWUGXUGPWWAWUGUYHT
        UYIXRWWMXTXAXQWXJVYQWXPVYHPWXJVYKVYQWYGVYSTVYHUYJTUYKUYLWXJWXMWXKVXLWBQ
        ZXGRZWUNWXJWYHWXMXVMVHWYKWXKYATWXJXVLWUMXGWXJWYIXVLWUMPZWYJWYIVXOVXQXVN
        VXRVXSWULVVOUHXHWQTYQUYMWXJVUIVYHVUFVVIVYOWUFVYPUYPUYQUYRUYSUYTYOCBGFIV
        UAVOGFIVUBVO $.
        $( [13-May-2014] $) $( [16-Mar-2008] $)
    $}

    efif1o.3 $e |- D = ( A (,] ( A + ( 2 x. pi ) ) ) $.
    $( The exponential function of an imaginary number maps any open-below,
       closed-above interval of length ` 2 pi ` one-to-one onto the unit
       circle.  (Contributed by Paul Chapman, 16-Mar-2008.) $)
    efif1o $p |- ( A e. RR -> F : D -1-1-onto-> C ) $=
      ( vx vy vz cr wcel csin cpi c2 cdiv co cneg wbr cicc cres cmul caddc cioc
      cv clt cle w3a cxr wb rexr 2re pire remulcli readdcl mpan2 elioc2 syl2anc
      simp1 syl6bi ssrdv syl5eqss efif1olem1 efif1olem2 eqid efif1olem3 ) BLMZI
      JKACDNOPQRZSVIUARUBZEFGVHDBBPOUCRZUDRZUERZLHVHIVMLVHIUFZVMMZVNLMZBVNUGTZV
      NVLUHTZUIZVPVHBUJMVLLMZVOVSUKBULVHVKLMVTPOUMUNUOBVKUPUQBVLVNURUSVPVQVRUTV
      AVBVCIJBDHVDJKBDHVEVJVFVG $.
      $( [13-May-2014] $) $( [16-Mar-2008] $)
  $}

  ${
    $d w x y z A $.  $d x y z C $.  $d x y z D $.  $d x y F $.  $d x y z ph $.
    $d y z S $.
    efifo.1 $e |- F = ( z e. RR |-> ( exp ` ( _i x. z ) ) ) $.
    efifo.2 $e |- C = ( `' abs " { 1 } ) $.
    $( The exponential function of an imaginary number maps the reals onto the
       unit circle.  (Contributed by Mario Carneiro, 13-May-2014.) $)
    efifo $p |- F : RR -onto-> C $=
      ( cr wfo wfn crn wceq wf ci co wcel cabs c1 cc ax-mp cc0 cioc df-fo cv ce
      cmul cfv wral ccnv csn cima ax-icn recn mulcl sylancr efcl absefi wa absf
      syl wb ffn ax-1cn fniniseg mp2an sylanbrc syl6eleqr rgen fmpt mpbi wss c2
      frn cpi cres cmpt df-ima reseq1 clt wbr cle cxr w3a 0xr 2re pire remulcli
      elioc2 simp1bi ssriv resmpt eqtri rneqi wf1o 0re eqid caddc recni addid2i
      oveq2i eqcomi efif1o f1ofo forn 3eqtri imassrn eqsstr3i eqssi mpbir2an
      mp2b ) FBCGCFHZCIZBJFBCUAFBCKZXILAUBZUDMZUCUEZBNZAFUFXKXOAFXLFNZXNOUGPUHU
      IZBXPXNQNZXNOUEPJZXNXQNZXPXMQNZXRXPLQNXLQNYAUJXLUKLXLULUMXMUNURXLUOOQHZPQ
      NXTXRXSUPUSQFOKYBUQQFOUTRVAQPXNOQVBVCVDEVEVFAFBXNCDVGVHZFBCUTRXJBXKXJBVIY
      CFBCVKRBCSVJVLUDMZTMZUIZXJYFCYEVMZIAYEXNVNZIZBCYEVOYGYHYGAFXNVNZYEVMZYHCY
      JJYGYKJDCYJYEVPRYEFVIYKYHJAYEFXLYENZXPSXLVQVRZXLYDVSVRZSVTNYDFNYLXPYMYNWA
      USWBVJVLWCWDWEZSYDXLWFVCWGWHAFYEXNWIRWJWKYEBYHWLZYEBYHGYIBJSFNYPWMASBYEYH
      YHWNESSYDWOMZTMYEYQYDSTYDYDYOWPWQWRWSWTRYEBYHXAYEBYHXBXHXCCYEXDXEXFXG $.
      $( [13-May-2014] $)
  $}

  ${
    $d x y C $.
    circgrp.1 $e |- C = ( `' abs " { 1 } ) $.
    circgrp.2 $e |- T = ( x. |` ( C X. C ) ) $.
    $( The circle group ` T ` is an Abelian group.  (Contributed by Paul
       Chapman, 25-Mar-2008.)  (Revised by Mario Carneiro, 13-May-2014.) $)
    circgrp $p |- T e. AbelOp $=
      ( vy vx cabl wcel wtru ci cr cv cmul co ce cfv cmpt wceq cc a1i wrex eqid
      crn cab wfo efifo forn ax-mp rnmpt eqtr3i wss ax-resscn readdsubg efghgrp
      ax-icn trud ) BGHIEFJABKEKJELMNOPZQZUCZAFLUQREKUAFUDKAURUEUSAREAURURUBZCU
      FKAURUGUHEFKUQURUTUIUJDJSHIUOTKSUKIULTUMUNUP $.
      $( [13-May-2014] $) $( [25-Mar-2008] $)
  $}

  ${
    $d w x y z D $.  $d x y z F $.  $d w x y z ph $.  $d x y S $.
    eff1olem.1 $e |- F = ( w e. D |-> ( exp ` ( _i x. w ) ) ) $.
    eff1olem.2 $e |- S = ( `' Im " D ) $.
    eff1olem.3 $e |- ( ph -> D C_ RR ) $.
    eff1olem.4 $e |- ( ( ph /\ ( x e. D /\ y e. D ) ) ->
                       ( abs ` ( x - y ) ) < ( 2 x. pi ) ) $.
    eff1olem.5 $e |- ( ( ph /\ z e. RR ) ->
                       E. y e. D ( ( z - y ) / ( 2 x. pi ) ) e. ZZ ) $.
    $( The exponential function maps the set ` S ` , of complex numbers with
       imaginary part in a real interval of length ` 2 x. pi ` , one-to-one
       onto the nonzero complex numbers.  (Contributed by Paul Chapman,
       16-Apr-2008.)  (The proof was shortened by Mario Carneiro,
       13-May-2014.) $)
    eff1olem $p |- ( ph -> ( exp |` S ) : S -1-1-onto-> ( CC \ { 0 } ) ) $=
      ( cc ce cfv cr co wceq wcel cc0 cdif cv cabs cres ccnv ci cdiv cmul caddc
      csn cmpt wf wfn eff2 ffn dffn5v reseq1 mp2b wss cim cima cdm cnvimass imf
      sylbi fdmi eqcomi 3sstr4i resmpt ax-mp eqtri sseli ffvelrni syl adantl wa
      crp clt wbr wne simpr eldifsn sylib simpld abscl cle wb 0re ltlen sylancr
      absge0 simprd abs00 necon3bid mpbird mpbir2and elrp sylanbrc wf1o reeff1o
      f1ocnv f1of recnd ax-icn adantr c1 csin c2 cneg cicc eqid efif1olem3 3syl
      cpi divcl syl3anc absdiv absidm oveq2d divid syl2anc mulcl elpreima efadd
      fvres f1ocnvfv2 eqtr3d oveq2 fveq2d fvmpt oveq12d fveq2 eqeq2d syl5ibrcom
      fvex eqtr4d f1ocnvfv1 eqtrd efcl 3eqtrd absf ax-1cn fniniseg mp2an sseldd
      ffvelrn addcl eqeltrd syl6eleqr cbvmptv divcan2 3eqtrrd adantrl wi replim
      crim cre absef recl imcl divcan3 simprbi eleq2s 3eqtr4d syl2an id adantrr
      efne0 impbid f1o2d ) ACBGNUAUKUBZCUCZOPZBUCZUDPZOQUEZUFZPZUGUVOUVPUHRZHUF
      ZPZUIRZUJRZOGUEZUWECNUVNULZGUEZCGUVNULZNUVLOUMONUNZUWEUWGSZUONUVLOUPUWIOU
      WFSUWJCNOUQOUWFGURVFUSGNUTUWGUWHSVAUFFVBZVAVCZGNVAFVDJUWLNNQVAVEVGVHVIZCN
      GUVNVJVKVLUVMGTZUVNUVLTZAUWNUVMNTZUWOGNUVMUWMVMZNUVLUVMOUOVNVOVPAUVOUVLTZ
      VQZUWDUWKGUWSUWDNTZUWDVAPZFTZUWDUWKTZUWSUVSNTZUWCNTZUWTUWSUVSUWSUVPVRTZUV
      SQTZUWSUVPQTZUAUVPVSVTZUXFUWSUVONTZUXHUWSUXJUVOUAWAZUWSUWRUXJUXKVQAUWRWBU
      VONUAWCWDZWEZUVOWFVOZUWSUXIUAUVPWGVTZUVPUAWAZUWSUAQTUXHUXIUXOUXPVQWHWIUXN
      UAUVPWJWKUWSUXJUXOUXMUVOWLVOUWSUXPUXKUWSUXJUXKUXLWMUWSUVPUAUVOUAUWSUXJUVP
      UASUVOUASWHUXMUVOWNVOWOWPZWQUVPWRWSZVRQUVPUVRQVRUVQWTZVRQUVRWTVRQUVRUMXAQ
      VRUVQXBVRQUVRXCUSVNVOZXDZUWSUGNTZUWBNTUXEXEUWSUWBUWSFQUWBAFQUTUWRKXFUWSUD
      UFXGUKVBZFUWAUMZUVTUYCTZUWBFTZAUYDUWRAFUYCHWTZUYCFUWAWTUYDABCDEUYCFXHXOXI
      UHRZXJUYHXKRUEZHIUYCXLKLMUYIXLXMZFUYCHXBUYCFUWAXCXNXFUWSUVTNTZUVTUDPZXGSZ
      UYEUWSUXJUVPNTZUXPUYKUXMUWSUVPUXNXDZUXQUVOUVPXPXQUWSUYLUVPUVPUDPZUHRZUVPU
      VPUHRZXGUWSUXJUYNUXPUYLUYQSUXMUYOUXQUVOUVPXRXQUWSUYPUVPUVPUHUWSUXJUYPUVPS
      UXMUVOXSVOXTUWSUYNUXPUYRXGSUYOUXQUVPYAYBUUAUDNUNZXGNTUYEUYKUYMVQWHNQUDUMU
      YSUUBNQUDUPVKUUCNXGUVTUDNUUDUUEWSZUYCFUVTUWAUUGYBZUUFZXDUGUWBYCWKZUVSUWCU
      UHYBUWSUXAUWBFUWSUXGUWBQTUXAUWBSUXTVUBUVSUWBUUQYBVUAUUINQVAUMZVANUNZUXCUW
      TUXBVQWHVENQVAUPZNUWDFVAYDUSWSJUUJAUWNUWRVQVQZUVMUWDSZUVOUVNSZVUGVUIVUHUV
      OUWDOPZSZAUWRVUKUWNUWSVUJUVSOPZUWCOPZUIRZUVPUVTUIRZUVOUWSUXDUXEVUJVUNSUYA
      VUCUVSUWCYEYBUWSVULUVPVUMUVTUIUWSUVSUVQPZVULUVPUWSUXGVUPVULSUXTUVSQOYFVOU
      WSUXSUXFVUPUVPSXAUXRQVRUVPUVQYGWKYHUWSUWBHPZVUMUVTUWSUYFVUQVUMSVUADUWBUGD
      UCZUIRZOPZVUMFHVURUWBSVUSUWCOVURUWBUGUIYIYJHEFUGEUCZUIRZOPZULDFVUTULIEDFV
      VCVUTVVAVURSVVBVUSOVVAVURUGUIYIYJUUKVLUWCOYPYKVOUWSUYGUYEVUQUVTSAUYGUWRUY
      JXFUYTFUYCUVTHYGYBYHYLUWSUXJUYNUXPVUOUVOSUXMUYOUXQUVOUVPUULXQUUMUUNVUHUVN
      VUJUVOUVMUWDOYMYNYOAUWNVUIVUHUUOUWRAUWNVQZVUHVUIUVMUVNUDPZUVRPZUGUVNVVEUH
      RZUWAPZUIRZUJRZSVVDUVMUVMUURPZUGUVMVAPZUIRZUJRZVVJVVDUWPUVMVVNSUWNUWPAUWQ
      VPZUVMUUPVOZVVDVVFVVKVVIVVMUJVVDVVFVVKUVQPZUVRPZVVKVVDVVEVVQUVRVVDVVEVVKO
      PZVVQVVDUWPVVEVVSSVVOUVMUUSVOZVVDVVKQTZVVQVVSSVVDUWPVWAVVOUVMUUTVOZVVKQOY
      FVOYQYJVVDUXSVWAVVRVVKSXAVWBQVRVVKUVQYRWKYSVVDVVHVVLUGUIVVDVVHVVLHPZUWAPZ
      VVLVVDVVGVWCUWAVVDVVSVVMOPZUIRZVVSUHRZVWEVVGVWCVVDVWENTZVVSNTZVVSUAWAZVWG
      VWESVVDVVMNTZVWHVVDUYBVVLNTVWKXEVVDVVLVVDUWPVVLQTVVOUVMUVAVOXDUGVVLYCWKZV
      VMYTVOVVDVVKNTZVWIVVDVVKVWBXDZVVKYTVOVVDVWMVWJVWNVVKUVIVOVWEVVSUVBXQVVDUV
      NVWFVVEVVSUHVVDUVNVVNOPZVWFVVDUVMVVNOVVPYJVVDVWMVWKVWOVWFSVWNVWLVVKVVMYEY
      BYSVVTYLVVDVVLFTZVWCVWESUWNVWPAVWPUVMUWKGUVMUWKTZUWPVWPVUDVUEVWQUWPVWPVQW
      HVEVUFNUVMFVAYDUSUVCJUVDZVPEVVLVVCVWEFHVVAVVLSVVBVVMOVVAVVLUGUIYIYJIVVMOY
      PYKVOUVEYJAUYGVWPVWDVVLSUWNUYJVWRFUYCVVLHYRUVFYSXTYLYQVUIUWDVVJUVMVUIUVSV
      VFUWCVVIUJVUIUVPVVEUVRUVOUVNUDYMZYJVUIUWBVVHUGUIVUIUVTVVGUWAVUIUVOUVNUVPV
      VEUHVUIUVGVWSYLYJXTYLYNYOUVHUVJUVK $.
      $( [13-May-2014] $) $( [16-Apr-2008] $)
  $}

  ${
    $d w x y z S $.
    eff1o.1 $e |- S = ( `' Im " ( -u pi (,] pi ) ) $.
    $( The exponential function maps the set ` S ` , of complex numbers with
       imaginary part in the closed-above, open-below interval from ` -u pi `
       to ` pi ` one-to-one onto the nonzero complex numbers.  (Contributed by
       Paul Chapman, 16-Apr-2008.)  (Revised by Mario Carneiro,
       13-May-2014.) $)
    eff1o $p |- ( exp |` S ) : S -1-1-onto-> ( CC \ { 0 } ) $=
      ( vx vy vz vw cpi cr wcel cc ce pire cioc co cmul wbr caddc recni oveq2i
      cv cneg cc0 csn cdif cres wf1o renegcli ci cfv cmpt eqid cxr wss rexr clt
      wa cle w3a elioc2 simp1 syl6bi ssrdv sylancl 2timesi addcomi cmin negsubi
      c2 addcli pncan mp2an eqtri 3eqtrri efif1olem1 efif1olem2 eff1olem ax-mp
      wceq ) GUAZHIZAJUBUCUDKAUEUFGLUGZVTCDEFVSGMNZAFWBUHFTONKUIUJZWCUKBVTVSULI
      ZGHIZWBHUMVSUNLWDWEUPZCWBHWFCTZWBIWGHIZVSWGUOPZWGGUQPZURWHVSGWGUSWHWIWJUT
      VAVBVCCDVSWBGVSVHGONZQNZVSMWLVSGGQNZQNWMVSQNZGWKWMVSQGGLRZVDSVSWMVSWARGGW
      OWOVIZVEWNWMGVFNZGWMGWPWOVGGJIZWRWQGVRWOWOGGVJVKVLVMSZVNDEVSWBWSVOVPVQ $.
      $( [13-May-2014] $) $( [16-Apr-2008] $)
  $}

