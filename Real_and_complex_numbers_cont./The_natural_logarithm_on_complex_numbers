$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The natural logarithm on complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c log $.

  $( Extend class notation with the natural logarithm function on complex
     numbers. $)
  clog $a class log $.

  $( Define the natural logarithm function on complex numbers.  See
     ~ http://en.wikipedia.org/wiki/Natural_logarithm ("The natural logarithm
     function can also be defined as the inverse function of the exponential
     function"). $)
  df-log $a |- log =
               `' ( exp |` { x e. CC | ( Im ` x ) e. ( -u pi [,) pi ) } ) $.

  $( The range of the natural logarithm function, also the principal domain of
     the exponential function.  This allows us to write the longer class
     abstraction as simply ` ran log ` .  (Contributed by Paul Chapman,
     21-Apr-2008.) $)
  logrn $p |- ran log = { x e. CC | ( Im ` x ) e. ( -u pi [,) pi ) } $=
    ( cc cc0 csn cdif cv cim cfv cpi cneg cico wcel crab clog wf1 crn wceq wf1o
    co ax-mp wa ce cres ccnv eff1o f1ocnv wb df-log f1oeq1 mpbir dff1o5 simpri
    mpbi ) BCDEZAFGHIJIKSLABMZNOZNPUOQZUNUONRZUPUQUAURUNUOUBUOUCZUDZRZUOUNUSRVA
    AUEUOUNUSUFTNUTQURVAUGAUHUNUONUTUITUJUNUONUKUMUL $.
    $( [21-Apr-2008] $)

  $( The natural logarithm function in terms of the exponential function
     restricted to its principal domain.  (Contributed by Paul Chapman,
     21-Apr-2008.) $)
  dflog2 $p |- log = `' ( exp |` ran log ) $=
    ( vx clog ce cv cim cfv cpi cneg cico co wcel cc crab cres crn df-log logrn
    ccnv reseq2i cnveqi eqtr4i ) BCADEFGHGIJKALMZNZRCBOZNZRAPUEUCUDUBCAQSTUA $.
    $( [21-Apr-2008] $)

  $( The range of the natural logarithm function includes the real numbers.
     (Contributed by Paul Chapman, 21-Apr-2008.) $)
  resslogrn $p |- RR C_ ran log $=
    ( vx cr clog crn cv wcel cim cfv cpi cneg cico co cc cc0 wbr clt pire pipos
    0re wb crab recn reim0 cle renegcli lt0neg2 ax-mp ltleii w3a elico2 biimpri
    mpbi mp2an mp3an syl6eqel rabid sylanbrc logrn syl6eleqr ssriv ) ABCDZAEZBF
    ZVBVBGHZIJZIKLZFZAMUAZVAVCVBMFVGVBVHFVBUBVCVDNVFVBUCNBFZVENUDOZNIPOZNVFFZSV
    ENIQUEZSVKVENPOZRIBFZVKVNTQIUFUGULUHRVLVIVJVKUIZVEBFVOVLVPTVMQVEINUJUMUKUNU
    OVGAMUPUQAURUSUT $.
    $( [21-Apr-2008] $)

  $( The exponential function restricted to its principal domain maps
     one-to-one onto the nonzero complex numbers.  (Contributed by Paul
     Chapman, 21-Apr-2008.) $)
  eff1o2 $p |- ( exp |` ran log ) : ran log -1-1-onto-> ( CC \ { 0 } ) $=
    ( vx clog crn cc cc0 csn cdif ce cres wf1o cv cim cfv cpi cneg cico wceq wb
    ax-mp mpbir co wcel crab eff1o logrn reseq2i f1oeq1 f1oeq2 ) BCZDEFGZHUIIZJ
    ZAKLMNONPUAUBADUCZUJUKJZUNUMUJHUMIZJZAUDUKUOQUNUPRUIUMHAUEZUFUMUJUKUOUGSTUI
    UMQULUNRUQUIUMUJUKUHST $.
    $( [21-Apr-2008] $)

  $( The natural logarithm function maps the nonzero complex numbers one-to-one
     onto its range.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  logf1o $p |- log : ( CC \ { 0 } ) -1-1-onto-> ran log $=
    ( cc cc0 csn cdif clog wf1o ce cres ccnv eff1o2 f1ocnv ax-mp wceq wb dflog2
    crn f1oeq1 mpbir ) ABCDZEPZEFZSTGTHZIZFZTSUBFUDJTSUBKLEUCMUAUDNOSTEUCQLR $.
    $( [21-Apr-2008] $)

  $( The natural logarithm function on the positive reals in terms of the real
     exponential function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  dfrelog $p |- ( log |` RR+ ) = `' ( exp |` RR ) $=
    ( ce clog crn cres ccnv cr cima crp df-ima wss wceq resslogrn resabs1 ax-mp
    rneqi wfn wfun wf1o mpbi dflog2 reeff1o dff1o2 simp3i 3eqtri reseq2i cnveqi
    w3a cc cc0 csn cdif logf1o f1ofun wb funeq funcnvres eqtr3i reseq1 3eqtr4ri
    ) ABCZDZEZVAFGZDZVBHDZAFDZEZBHDZVCHVBVCVAFDZCVFCZHVAFIVIVFFUTJVIVFKLAFUTMNZ
    OVFFPZVGQZVJHKZFHVFRVLVMVNUGUAFHVFUBSUCUDUEVIEZVGVDVIVFVKUFVBQZVOVDKBQZVPUH
    UIUJUKZUTBRVQULVRUTBUMNBVBKZVQVPUNTBVBUONSFVAUPNUQVSVHVEKTBVBHURNUS $.
    $( [21-Apr-2008] $)

  $( The natural logarithm function maps the positive reals one-to-one onto the
     real numbers.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  relogf1o $p |- ( log |` RR+ ) : RR+ -1-1-onto-> RR $=
    ( crp cr clog cres wf1o ce crn ccnv wfun cc0 csn cdif eff1o2 wfo ax-mp wceq
    cc wb f1oeq1 mpbir dff1o3 simprbi reeff1o resslogrn resabs1 f1orescnv mp2an
    wss dflog2 reseq1 ) ABCADZEZABFCGZDZHZADZEZUOIZBAUNBDZEZUQUMQJKLZUNEZURMVBU
    MVAUNNURUMVAUNUAUBOUTBAFBDZEZUCUSVCPZUTVDRBUMUHVEUDFBUMUEOBAUSVCSOTABUNUFUG
    UKUPPZULUQRCUOPVFUICUOAUJOABUKUPSOT $.
    $( [21-Apr-2008] $)

  $( Closure of the natural logarithm function.  (Contributed by Paul Chapman,
     21-Apr-2008.) $)
  logcl $p |- ( ( A e. CC /\ A =/= 0 ) -> ( log ` A ) e. ran log ) $=
    ( cc wcel cc0 wne wa csn cdif clog cfv eldifsn wf1o wf logf1o f1of ffvelrni
    crn ax-mp sylbir ) ABCADEFABDGHZCAIJIQZCABDKTUAAITUAILTUAIMNTUAIORPS $.
    $( [21-Apr-2008] $)

  $( Closure of the natural logarithm function on positive reals.  (Contributed
     by Steve Rodriguez, 25-Nov-2007.) $)
  relogcl $p |- ( A e. RR+ -> ( log ` A ) e. RR ) $=
    ( crp wcel clog cres cr fvres wf1o wf relogf1o f1of ax-mp ffvelrni eqeltrrd
    cfv ) ABCADBEZOADOFABDGBFAPBFPHBFPIJBFPKLMN $.
    $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  eflog $p |- ( ( A e. CC /\ A =/= 0 ) -> ( exp ` ( log ` A ) ) = A ) $=
    ( cc wcel cc0 wne wa clog cfv ce crn cres wceq logcl syl ccnv dflog2 fveq1i
    fvres fveq2i csn cdif eldifsn wf1o eff1o2 f1ocnvfv2 sylbir syl5eq eqtr3d
    mpan ) ABCADEFZAGHZIGJZKZHZUKIHZAUJUKULCUNUOLAMUKULIRNUJUNAUMOZHZUMHZAUKUQU
    MAGUPPQSUJABDTUAZCZURALZABDUBULUSUMUCUTVAUDULUSAUMUEUIUFUGUH $.
    $( [21-Apr-2008] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  reeflog $p |- ( A e. RR+ -> ( exp ` ( log ` A ) ) = A ) $=
    ( crp wcel cc cc0 wne wa clog cfv ce wceq rpcnne0 eflog syl ) ABCADCAEFGAHI
    JIAKALAMN $.
    $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  logef $p |- ( A e. ran log -> ( log ` ( exp ` A ) ) = A ) $=
    ( clog crn wcel ce cres cfv fvres fveq2d ccnv dflog2 fveq1i cc cc0 csn cdif
    wf1o wceq eff1o2 f1ocnvfv1 mpan syl5eq eqtr3d ) ABCZDZAEUDFZGZBGZAEGZBGAUEU
    GUIBAUDEHIUEUHUGUFJZGZAUGBUJKLUDMNOPZUFQUEUKARSUDULAUFTUAUBUC $.
    $( [21-Apr-2008] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogef $p |- ( A e. RR -> ( log ` ( exp ` A ) ) = A ) $=
    ( cr wcel clog crn ce cfv wceq resslogrn sseli logef syl ) ABCADEZCAFGDGAHB
    MAIJAKL $.
    $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  logeftb $p |- ( ( A e. CC /\ A =/= 0 /\ B e. ran log ) ->
                  ( ( log ` A ) = B <-> ( exp ` B ) = A ) ) $=
    ( cc wcel cc0 wne clog crn cfv wceq ce wb csn cdif eldifsn cres ccnv eqeq1d
    wa fvres adantr eff1o2 f1ocnvfvb mp3an1 bitr3d ancoms dflog2 fveq1i sylanbr
    wf1o eqeq1i syl6rbbr 3impa ) ACDZAEFZBGHZDZAGIZBJZBKIZAJZLZUNUOSACEMNZDZUQV
    BACEOVDUQSVAAKUPPZQZIZBJZUSUQVDVAVHLUQVDSBVEIZAJZVAVHUQVJVALVDUQVIUTABUPKTR
    UAUPVCVEUJUQVDVJVHLUBUPVCBAVEUCUDUEUFURVGBAGVFUGUHUKULUIUM $.
    $( [21-Apr-2008] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogeftb $p |- ( ( A e. RR+ /\ B e. RR ) ->
                    ( ( log ` A ) = B <-> ( exp ` B ) = A ) ) $=
    ( crp wcel cc cc0 wne wa clog crn cfv wceq ce wb cr rpcnne0 resslogrn sseli
    logeftb 3expa syl2an ) ACDAEDZAFGZHBIJZDZAIKBLBMKALNZBODAPOUDBQRUBUCUEUFABS
    TUA $.
    $( [25-Nov-2007] $)

  $( The natural logarithm of ` 1 ` .  One case of Property 1a of [Cohen]
     p. 301.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  log1 $p |- ( log ` 1 ) = 0 $=
    ( c1 clog cfv cc0 wceq ce ef0 crp wcel cr wb 1rp 0re relogeftb mp2an mpbir
    ) ABCDEZDFCAEZGAHIDJIQRKLMADNOP $.
    $( [25-Nov-2007] $)

  $( The natural logarithm of ` _e ` .  One case of Property 1b of [Cohen]
     p. 301.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  loge $p |- ( log ` _e ) = 1 $=
    ( ceu clog cfv c1 wceq ce df-e eqcomi crp wcel cr wb ere cc0 clt 1re efgt0i
    breqtrri elrpii relogeftb mp2an mpbir ) ABCDEZDFCZAEZAUDGHAIJDKJUCUELAMNUDA
    ODPQGRSPADTUAUB $.
    $( [25-Nov-2007] $)

  $( Relationship between ` pi ` and the natural logarithm function.
     (Contributed by Paul Chapman, 21-Apr-2008.) $)
  pilog $p |- pi = ( _i x. ( log ` -u 1 ) ) $=
    ( vx ci c1 cneg cfv cmul co cpi wceq ce caddc cc wcel ax-icn pire ax-mp cc0
    cmin cim wbr clog c2 cz recni mulcli znegcl efper mp2an 2cn negsubi mulcomi
    zcn mulm1i eqtri opreq2i subdii 3eqtr4i negsubdi2i 2timesi eqcomi subaddrii
    1z negeqi eqtr3i mulneg2i 3eqtri fveq2i df-neg eulerid ax-1cn efcl subadd2i
    0cn mpbir eqtr2i 3eqtr3i wne crn wb ax-1ne0 negne0i cico fveq2 eleq1d elrab
    cv crab negcli imnegi addid2i 0re crimi cr cle renegcli leidi pipos lt0neg2
    clt mpbi lttri elico2 biimpri mp3an eqeltri mpbir2an logrn eleqtrri logeftb
    w3a ixi opreq1i mulassi negnegi ) BCDZUAEZFGBBHFGZDZFGZHXPXRBFXPXRIZXRJEZXO
    IZXQBUBHFGZFGZXOFGZKGZJEZXQJEZYAXOXQLMZXOUCMZYGYHIBHNHOUDZUEZCUCMYJVBCUFPZX
    QXOUGUHYFXRJYFBHYCRGZFGZBHDZFGXRXQYDDZKGXQYDRGYFYOXQYDYLBYCNUBHUIYKUEZUEZUJ
    YEYQXQKYEXOYDFGYQYDXOYSYJXOLMZYMXOULPZUKYDYSUMUNUOBHYCNYKYRUPUQYNYPBFYCHRGZ
    DYNYPYCHYRYKURUUBHYCHHYRYKYKYCHHKGHYKUSUTVAVCVDUOBHNYKVEVFVGXOQCRGZYHCVHUUC
    YHIYHCKGQIVIQCYHVMVJYIYHLMYLXQVKPVLVNVOVPYTXOQVQXRUAVRZMXTYBVSUUACVJVTWAXRA
    WFZSEZYPHWBGZMZALWGZUUDXRUUIMXRLMXRSEZUUGMZUUHUUKAXRLUUEXRIUUFUUJUUGUUEXRSW
    CWDWEXQYLWHUUJYPUUGUUJXQSEZDYPXQYLWIUULHQXQKGZSEUULHUUMXQSXQYLWJVGQHWKOWLVD
    VCUNYPWMMZYPYPWNTZYPHWSTZYPUUGMZHOWOZYPUURWPYPQWSTZQHWSTZUUPUUTUUSWQHWMMZUU
    TUUSVSOHWRPWTWQYPQHUURWKOXAUHUUQUUNUUOUUPXJZUUNUVAUUQUVBVSUUROYPHYPXBUHXCXD
    XEXFAXGXHXOXRXIXDVNUOXSBXQFGZDYPDHBXQNYLVEUVCYPBBFGZHFGXOHFGUVCYPUVDXOHFXKX
    LBBHNNYKXMHYKUMVPVCHYKXNVFVO $.
    $( [21-Apr-2008] $)

  ${
    relogoprlem.1 $e |- ( ( ( log ` A ) e. CC /\ ( log ` B ) e. CC ) ->
                        ( exp ` ( ( log ` A ) F ( log ` B ) ) ) =
                        ( ( exp ` ( log ` A ) ) G ( exp ` ( log ` B ) ) ) ) $.
    relogoprlem.2 $e |- ( ( ( log ` A ) e. RR /\ ( log ` B ) e. RR ) ->
                          ( ( log ` A ) F ( log ` B ) ) e. RR ) $.
    $( Lemma for ~ relogmul and ~ relogdiv .  Remark of [Cohen] p. 301 ("The
       proof of Property 3 is quite similar to the proof given for Property
       2"). $)
    relogoprlem $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
        ( log ` ( A G B ) ) = ( ( log ` A ) F ( log ` B ) ) ) $=
      ( crp wcel wa clog cfv ce co reeflog fveq2d cr wceq relogcl cc recn syl
      opreqan12d syl2an relogef eqtr3d ) AGHZBGHZIZAJKZLKZBJKZLKZDMZJKZABDMZJKU
      IUKCMZUHUMUOJUFUGUJAULBDANBNUBOUFUIPHZUKPHZUNUPQUGARBRUQURIZUPLKZJKZUNUPU
      QUISHZUKSHZVAUNQURUITUKTVBVCIUTUMJEOUCUSUPPHVAUPQFUPUDUAUEUCUE $.
      $( [25-Nov-2007] $)
  $}

  $( The natural logarithm of the product of two positive real numbers is the
     sum of natural logarithms.  Property 2 of [Cohen] p. 301, restricted to
     natural logarithms.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogmul $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( log ` ( A x. B ) ) = ( ( log ` A ) + ( log ` B ) ) ) $=
    ( caddc cmul clog cfv efadd readdcl relogoprlem ) ABCDAEFZBEFZGJKHI $.
    $( [25-Nov-2007] $)

  $( The natural logarithm of the quotient of two positive real numbers is the
     difference of natural logarithms.  Exercise 72(a) and Property 3 of
     [Cohen] p. 301, restricted to natural logarithms.  (Contributed by Steve
     Rodriguez, 25-Nov-2007.) $)
  relogdiv $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( log ` ( A / B ) ) = ( ( log ` A ) - ( log ` B ) ) ) $=
    ( cmin cdiv clog cfv efsub resubcl relogoprlem ) ABCDAEFZBEFZGJKHI $.
    $( [25-Nov-2007] $)

  $( Exponentiation of a nonzero complex number to a nonnegative integer
     power.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  explog $p |- ( ( A e. CC /\ A =/= 0 /\ N e. NN0 ) ->
                  ( A ^ N ) = ( exp ` ( N x. ( log ` A ) ) ) ) $=
    ( vx cc wcel cc0 wne cn0 w3a clog cfv cmul co ce cexp wceq wa crn logcl cpi
    cim cneg cico crab logrn ssrab2 eqsstri sseli syl efexp sylan 3impa opreq1d
    cv eflog 3adant3 eqtr2d ) ADEZAFGZBHEZIBAJKZLMNKZVANKZBOMZABOMZURUSUTVBVDPZ
    URUSQZVADEZUTVFVGVAJRZEVHASVIDVAVICUNUAKTUBTUCMEZCDUDDCUEVJCDUFUGUHUIVABUJU
    KULURUSVDVEPUTVGVCABOAUOUMUPUQ $.
    $( [21-Apr-2008] $)

  $( Exponentiation of a positive real number to a nonnegative integer power.
     (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  reexplog $p |- ( ( A e. RR+ /\ N e. NN0 ) ->
                    ( A ^ N ) = ( exp ` ( N x. ( log ` A ) ) ) ) $=
    ( crp wcel cn0 wa clog cfv cmul co ce cexp wceq relogcl recnd efexp reeflog
    cc sylan opreq1d adantr eqtr2d ) ACDZBEDZFBAGHZIJKHZUEKHZBLJZABLJZUCUERDUDU
    FUHMUCUEANOUEBPSUCUHUIMUDUCUGABLAQTUAUB $.
    $( [25-Nov-2007] $)

  $( The natural logarithm of positive ` A ` raised to an nonnegative integer
     power.  Property 4 of [Cohen] p. 301-302, restricted to natural logarithms
     and nonnegative-integer powers ` N ` .  (Contributed by Steve Rodriguez,
     25-Nov-2007.) $)
  relogexp $p |- ( ( A e. RR+ /\ N e. NN0 ) ->
                    ( log ` ( A ^ N ) ) = ( N x. ( log ` A ) ) ) $=
    ( crp wcel cn0 wa clog cfv cmul co ce cexp wceq relogcl recnd efexp reeflog
    cc sylan cr opreq1d adantr eqtrd fveq2d nn0re remulcl ancoms syl2an relogef
    syl eqtr3d ) ACDZBEDZFZBAGHZIJZKHZGHZABLJZGHUPUNUQUSGUNUQUOKHZBLJZUSULUORDU
    MUQVAMULUOANZOUOBPSULVAUSMUMULUTABLAQUAUBUCUDUNUPTDZURUPMULUOTDZBTDZVCUMVBB
    UEVEVDVCBUOUFUGUHUPUIUJUK $.
    $( [25-Nov-2007] $)

  $( The natural logarithm function on positive reals determines an isomorphism
     from the positive reals onto the reals.  (Contributed by Steve Rodriguez,
     25-Nov-2007.) $)
  relogiso $p |- ( log |` RR+ ) Isom < , < ( RR+ , RR ) $=
    ( crp cr clog cres wiso ce ccnv reefiso isocnv ax-mp wceq wb dfrelog isoeq1
    clt mpbir ) ABOOCADZEZABOOFBDZGZEZBAOOSEUAHBAOOSIJQTKRUALMABOOTQNJP $.
    $( [25-Nov-2007] $)

  ${
    $d A x y $.  $d B y $.
    $( The natural logarithm function on positive reals is strictly monotonic.
       (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
    logltb $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( A < B <-> ( log ` A ) < ( log ` B ) ) ) $=
      ( vx vy crp wcel wa clt wbr clog cres cv wb wral wceq fveq2 bibi12d fvres
      cfv cr wf1o wiso relogiso df-iso simpri breq1 breq1d breq2 breq2d rcla42v
      mpbi mpi breqan12d bitrd ) AEFZBEFZGZABHIZAJEKZSZBUSSZHIZAJSZBJSZHIUQCLZD
      LZHIZVEUSSZVFUSSZHIZMZDENCENZURVBMZETUSUAZVLETHHUSUBVNVLGUCCDETHHUSUDUKUE
      VKVMAVFHIZUTVIHIZMCDABEEVEAOZVGVOVJVPVEAVFHUFVQVHUTVIHVEAUSPUGQVFBOZVOURV
      PVBVFBAHUHVRVIVAUTHVFBUSPUIQUJULUOUPUTVCVAVDHAEJRBEJRUMUN $.
      $( [25-Nov-2007] $)
  $}

  ${
    $d A x y $.  $d B y $.
    $( The ` log x / x ` function is strictly decreasing on the reals greater
       than ` _e ` .  (Contributed by Mario Carneiro, 14-Mar-2014.) $)
    logdivlti $p |- ( ( ( A e. RR /\ B e. RR /\ _e <_ A ) /\ A < B )
                      -> ( ( log ` B ) / B ) < ( ( log ` A ) / A ) ) $=
      ( cr wcel ceu cle wbr clt cfv co cmul cmin c1 cc0 syl2anc syl ce wceq cc
      wb w3a wa clog crp simpl2 simpl3 simpr wi simpl1 ere lelttr mp3an1 mp2and
      cdiv 2pos ege2 0re 2re ltletri mp2an lttr mp3an12 mpani mpd elrp sylanbrc
      c2 ltletr rpdivcl relogcl rerpdivcl 1re resubcl sylancl ax-mulrcl reeflog
      caddc ax-1cn recnd pncan3 sylancr eqtr4d eqbrtrd ltmuldiv syl112anc mpbid
      mulid2 a1i posdif breq2 opreq2 fveq2 breq12d imbi12d elimel efgt1pi dedth
      cif sylc eflt mpbird mulid1 df-e breqtrrd syl5eqbrr efle eqbrtrrd ltletrd
      lemul2 relogdiv subdir syl3anc rpne0 div23 divass eqtr3d opreq12d 3brtr3d
      wne eqtrd ltsub1 ltdivmul ) ACDZBCDZEAFGZUAZABHGZUBZBUCIZBUNJAUCIZAUNJZHG
      ZYIBYKKJZHGZYHYNYIYJLJZYMYJLJZHGZYHBAUNJZUCIZYRMLJZYJKJZYOYPHYHYSYTUUAYHY
      RUDDZYSCDZYHBUDDZAUDDZUUBYHYDNBHGZUUDYCYDYEYGUEZYHEBHGZUUFYHYEYGUUHYCYDYE
      YGUFZYFYGUGZYHYCYDYEYGUBUUHUHZYCYDYEYGUIZUUGECDZYCYDUUKUJEABUKULOUMYHNEHG
      ZUUHUUFNVGHGVGEFGUUNUOUPNVGEUQURUJUSUTZYHYDUUNUUHUBUUFUHZUUGNCDZUUMYDUUPU
      QUJNEBVAVBPVCVDZBVEVFZYHYCNAHGZUUEUULYHYEUUTUUIYHUUNYEUUTUUOYHYCUUNYEUBUU
      TUHZUULUUQUUMYCUVAUQUJNEAVHVBPVCVDZAVEVFZBAVIOZYRVJPZYHYRCDZMCDZYTCDZYHYD
      UUEUVFUUGUVCBAVKOZVLYRMVMVNZYHUVHYJCDZUUACDUVJYHUUEUVKUVCAVJPZYTYJVOOYHYS
      YTHGZYSQIZYTQIZHGZYHUVNMYTVQJZUVOHYHUVNYRUVQYHUUBUVNYRRUVDYRVPPYHMSDZYRSD
      ZUVQYRRVRYHYRUVIVSZMYRVTWAWBYHUVHNYTHGZUVQUVOHGZUVJYHMYRHGZUWAYHMAKJZBHGZ
      UWCYHUWDABHYHASDZUWDARYHAUULVSZAWGPUUJWCYHUVGYDYCUUTUWEUWCTUVGYHVLWHZUUGU
      ULUVBMBAWDWEWFYHUVGUVFUWCUWATUWHUVIMYRWIOWFZUVHUWAUWBUHNUVHYTNWRZHGZMUWJV
      QJZUWJQIZHGZUHYTNYTUWJRZUWAUWKUWBUWNYTUWJNHWJUWOUVQUWLUVOUWMHYTUWJMVQWKYT
      UWJQWLWMWNUWJYTNCUQWOWPWQWSWCYHUUCUVHUVMUVPTUVEUVJYSYTWTOXAYHYTMKJZYTUUAF
      YHYTSDUWPYTRYHYTUVJVSYTXBPYHMYJFGZUWPUUAFGZYHUWQMQIZYJQIZFGZYHUWSEUWTFXCY
      HEAUWTFUUIYHUUEUWTARUVCAVPPXDXEYHUVGUVKUWQUXATUWHUVLMYJXFOXAYHUVGUVKUVHUW
      AUWQUWRTUWHUVLUVJUWIMYJYTXIWEWFXGXHYHUUDUUEYSYORUUSUVCBAXJOYHUUAYRYJKJZMY
      JKJZLJZYPYHUVSUVRYJSDZUUAUXDRUVTUVRYHVRWHYHYJUVLVSZYRMYJXKXLYHUXBYMUXCYJL
      YHBYJKJAUNJZUXBYMYHBSDZUXEUWFANXSZUXGUXBRYHBUUGVSZUXFUWGYHUUEUXIUVCAXMPZB
      YJAXNWEYHUXHUXEUWFUXIUXGYMRUXJUXFUWGUXKBYJAXOWEXPYHUXEUXCYJRUXFYJWGPXQXTX
      RYHYICDZYMCDZUVKYNYQTYHUUDUXLUUSBVJPZYHYDYKCDZUXMUUGYHUVKUUEUXOUVLUVCYJAV
      KOZBYKVOOUVLYIYMYJYAXLXAYHUXLUXOYDUUFYLYNTUXNUXPUUGUURYIYKBYBWEXA $.
      $( [14-Mar-2014] $)

    $( The ` log x / x ` function is strictly decreasing on the reals greater
       than ` _e ` .  (Contributed by Mario Carneiro, 14-Mar-2014.) $)
    logdivlt $p |- ( ( ( A e. RR /\ _e <_ A ) /\ ( B e. RR /\ _e <_ B ) ) ->
                   ( A < B <-> ( ( log ` B ) / B ) < ( ( log ` A ) / A ) ) ) $=
      ( cr wcel ceu cle wbr wa clt clog cfv cdiv co wi w3a logdivlti cc0 c2 0re
      ere ex 3expa an32s adantrr wceq wo wn fveq2 id opreq12d eqcomd a1i ancoms
      orim12d con3d wb crp simpl 2pos ege2 2re ltletri mp2an ltletr mp3an12 imp
      mpani elrp sylanbrc relogcl rerpdivcl mpancom syl axlttri syl2anr 3imtr4d
      ad2ant2r impbid ) ACDZEAFGZHZBCDZEBFGZHZHZABIGZBJKZBLMZAJKZALMZIGZWAWBWFW
      KNZWCVSWBVTWLVSWBVTWLVSWBVTOWFWKABPUAUBUCUDWEWHWJUEZWJWHIGZUFZUGZABUEZBAI
      GZUFZUGZWKWFWEWSWOWEWQWMWRWNWQWMNWEWQWJWHWQWIWGABLABJUHWQUIUJUKULWDWAWRWN
      NZWDVSXAVTWBVSWCXAWBVSWCXAWBVSWCOWRWNBAPUAUBUCUDUMUNUOWDWHCDZWJCDZWKWPUPW
      AWDBUQDZXBWDWBQBIGZXDWBWCURWBWCXEWBQEIGZWCXEQRIGREFGXFUSUTQRESVATVBVCZQCD
      ZECDZWBXFWCHXENSTQEBVDVEVGVFBVHVIWGCDXDXBBVJWGBVKVLVMWAAUQDZXCWAVSQAIGZXJ
      VSVTURVSVTXKVSXFVTXKXGXHXIVSXFVTHXKNSTQEAVDVEVGVFAVHVIWICDXJXCAVJWIAVKVLV
      MWHWJVNVOVSWBWFWTUPVTWCABVNVQVPVR $.
      $( [14-Mar-2014] $)
  $}


