$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The natural logarithm on complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c log $.

  $( Extend class notation with the natural logarithm function on complex
     numbers. $)
  clog $a class log $.

  $( Define the natural logarithm function on complex numbers.  See
     ~ http://en.wikipedia.org/wiki/Natural_logarithm ("The natural logarithm
     function can also be defined as the inverse function of the exponential
     function"). $)
  df-log $a |- log =
               `' ( exp |` { x e. CC | ( Im ` x ) e. ( -u pi [,) pi ) } ) $.

  $( The range of the natural logarithm function, also the principal domain of
     the exponential function.  This allows us to write the longer class
     abstraction as simply ` ran log ` .  (Contributed by Paul Chapman,
     21-Apr-2008.) $)
  logrn $p |- ran log = { x e. CC | ( Im ` x ) e. ( -u pi [,) pi ) } $=
    ( cc cc0 csn cdif cv cim cfv cpi cneg cico co wcel crab clog wf1 crn wceq
    wf1o wa ce cres ccnv eff1o f1ocnv ax-mp wb df-log f1oeq1 mpbir dff1o5 mpbi
    pm3.27i ) BCDEZAFGHIJIKLMABNZOPZOQUORZUNUOOSZUPUQTURUNUOUAUOUBZUCZSZUOUNUSS
    VAAUDUOUNUSUEUFOUTRURVAUGAUHUNUOOUTUIUFUJUNUOOUKULUM $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( The natural logarithm function in terms of the exponential function
     restricted to its principal domain.  (Contributed by Paul Chapman,
     21-Apr-2008.) $)
  dflog2 $p |- log = `' ( exp |` ran log ) $=
    ( vx clog ce cv cim cfv cpi cneg cico co wcel cc crab cres ccnv crn df-log
    wceq logrn reseq2 ax-mp cnveqi eqtr4i ) BCADEFGHGIJKALMZNZOCBPZNZOAQUGUEUFU
    DRUGUERASUFUDCTUAUBUC $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( The range of the natural logarithm function includes the real numbers.
     (Contributed by Paul Chapman, 21-Apr-2008.) $)
  resslogrn $p |- RR C_ ran log $=
    ( vx cr clog crn cv wcel cim cfv cpi cneg cico co cc crab wa rabid biimpri
    recn cc0 reim0 cle wbr clt 0re pire renegcli pipos wb lt0neg2 ax-mp mpbi
    ltleii w3a elico2 mp2an mp3an syl6eqel sylanc logrn syl6eleqr ssriv ) ABCDZ
    AEZBFZVCVCGHZIJZIKLZFZAMNZVBVCMFZVHVCVIFZVDVKVJVHOVHAMPQVCRVDVESVGVCTSBFZVF
    SUAUBZSIUCUBZSVGFZUDVFSIUEUFZUDVNVFSUCUBZUGIBFZVNVQUHUEIUIUJUKULUGVOVLVMVNU
    MZVFBFVRVOVSUHVPUEVFISUNUOQUPUQURAUSUTVA $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( The exponential function restricted to its principal domain maps
     one-to-one onto the nonzero complex numbers.  (Contributed by Paul
     Chapman, 21-Apr-2008.) $)
  eff1o2 $p |- ( exp |` ran log ) : ran log -1-1-onto-> ( CC \ { 0 } ) $=
    ( vx clog crn cc cc0 csn cdif ce cres wf1o cv cim cfv cpi cneg cico co
    wcel crab eff1o wceq wb logrn reseq2 ax-mp f1oeq1 mpbir f1oeq2 ) BCZDEFGZHU
    IIZJZAKLMNONPQRADSZUJUKJZUNUMUJHUMIZJZATUKUOUAZUNUPUBUIUMUAZUQAUCZUIUMHUDUE
    UMUJUKUOUFUEUGURULUNUBUSUIUMUJUKUHUEUG $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( The natural logarithm function maps the nonzero complex numbers one-to-one
     onto its range.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  logf1o $p |- log : ( CC \ { 0 } ) -1-1-onto-> ran log $=
    ( cc cc0 csn cdif clog crn wf1o ce cres ccnv eff1o2 f1ocnv ax-mp wceq wb
    dflog2 f1oeq1 mpbir ) ABCDZEFZEGZSTHTIZJZGZTSUBGUDKTSUBLMEUCNUAUDOPSTEUCQMR
    $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( The natural logarithm function on the positive reals in terms of the real
     exponential function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  dfrelog $p |- ( log |` RR+ ) = `' ( exp |` RR ) $=
    ( ce clog crn cres ccnv cr cima crp wceq df-ima wss resslogrn resabs1
    ax-mp rneqi wfn wfun wf1o w3a reeff1o2 dff1o2 mpbi 3simp3i 3eqtri reseq2
    cnveqi cc cc0 csn cdif logf1o f1ofun wb dflog2 funeq funcnvres eqtr3i
    reseq1 3eqtr4ri ) ABCZDZEZVAFGZDZVBHDZAFDZEZBHDZVCHIVDVEIVCVAFDZCVFCZHVAFJV
    IVFFUTKVIVFILAFUTMNZOVFFPZVGQZVJHIZFHVFRVLVMVNSTFHVFUAUBUCUDVCHVBUENVIEZVGV
    DVIVFVKUFVBQZVOVDIBQZVPUGUHUIUJZUTBRVQUKVRUTBULNBVBIZVQVPUMUNBVBUONUBFVAUPN
    UQVSVHVEIUNBVBHURNUS $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( The natural logarithm function maps the positive reals one-to-one onto the
     real numbers.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  relogf1o $p |- ( log |` RR+ ) : RR+ -1-1-onto-> RR $=
    ( crp cr clog cres wf1o ce crn ccnv wfun cc cc0 csn cdif eff1o2 wfo dff1o3
    pm3.27bi ax-mp reeff1o2 wceq wb wss resslogrn resabs1 f1oeq1 mpbir
    f1orescnv mp2an dflog2 reseq1 ) ABCADZEZABFCGZDZHZADZEZUOIZBAUNBDZEZUQUMJKL
    MZUNEZURNVBUMVAUNOURUMVAUNPQRUTBAFBDZEZSUSVCTZUTVDUABUMUBVEUCFBUMUDRBAUSVCU
    ERUFABUNUGUHUKUPTZULUQUACUOTVFUICUOAUJRABUKUPUERUF $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( Closure of the natural logarithm function.  (Contributed by Paul Chapman,
     21-Apr-2008.) $)
  logcl $p |- ( ( A e. CC /\ A =/= 0 ) -> ( log ` A ) e. ran log ) $=
    ( cc wcel cc0 wne wa csn cdif clog cfv crn eldifsn wf1o wf logf1o f1of
    ax-mp ffvelrni sylbir ) ABCADEFABDGHZCAIJIKZCABDLTUAAITUAIMTUAINOTUAIPQRS
    $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( Closure of the natural logarithm function on positive reals.  (Contributed
     by Steve Rodriguez, 25-Nov-2007.) $)
  relogcl $p |- ( A e. RR+ -> ( log ` A ) e. RR ) $=
    ( crp wcel clog cres cfv cr fvres wf1o wf relogf1o f1of ax-mp ffvelrni
    eqeltrrd ) ABCADBEZFADFGABDHBGAPBGPIBGPJKBGPLMNO $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  eflog $p |- ( ( A e. CC /\ A =/= 0 ) -> ( exp ` ( log ` A ) ) = A ) $=
    ( cc wcel cc0 wne wa clog cfv ce crn cres wceq logcl fvres syl ccnv csn
    cdif eldifsn wf1o eff1o2 f1ocnvfv2 mpan sylbir dflog2 fveq1i fveq2i syl5eq
    eqtr3d ) ABCADEFZAGHZIGJZKZHZUKIHZAUJUKULCUNUOLAMUKULINOUJAUMPZHZUMHZAUNUJA
    BDQRZCZURALZABDSULUSUMTUTVAUAULUSAUMUBUCUDUKUQUMAGUPUEUFUGUHUI $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  reeflog $p |- ( A e. RR+ -> ( exp ` ( log ` A ) ) = A ) $=
    ( crp wcel cc cc0 wne wa clog cfv ce wceq rpcnne0 eflog syl ) ABCADCAEFGAHI
    JIAKALAMN $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  logef $p |- ( A e. ran log -> ( log ` ( exp ` A ) ) = A ) $=
    ( clog crn wcel ce cres cfv fvres fveq2d ccnv cc cc0 csn cdif wf1o wceq
    eff1o2 f1ocnvfv1 mpan dflog2 fveq1i syl5eq eqtr3d ) ABCZDZAEUDFZGZBGZAEGZBG
    AUEUGUIBAUDEHIUEUGUFJZGZAUHUDKLMNZUFOUEUKAPQUDULAUFRSUGBUJTUAUBUC $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogef $p |- ( A e. RR -> ( log ` ( exp ` A ) ) = A ) $=
    ( cr wcel clog crn ce cfv wceq resslogrn sseli logef syl ) ABCADEZCAFGDGAHB
    MAIJAKL $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  logeftb $p |- ( ( A e. CC /\ A =/= 0 /\ B e. ran log ) ->
                  ( ( log ` A ) = B <-> ( exp ` B ) = A ) ) $=
    ( cc wcel cc0 wne clog crn cfv wceq ce wb csn cdif wa cres ccnv fvres
    eqeq1d adantr wf1o eff1o2 f1ocnvfvb mp3an1 bitr3d ancoms dflog2 fveq1i
    eqeq1i syl6rbbr eldifsn sylanbr 3impa ) ACDZAEFZBGHZDZAGIZBJZBKIZAJZLZACEMN
    ZDZUQVBUNUOOVDUQOVAAKUPPZQZIZBJZUSUQVDVAVHLUQVDOBVEIZAJZVAVHUQVJVALVDUQVIUT
    ABUPKRSTUPVCVEUAUQVDVJVHLUBUPVCBAVEUCUDUEUFURVGBAGVFUGUHUIUJACEUKULUM $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogeftb $p |- ( ( A e. RR+ /\ B e. RR ) ->
                    ( ( log ` A ) = B <-> ( exp ` B ) = A ) ) $=
    ( cc wcel cc0 wne wa clog crn cfv wceq ce wb crp cr logeftb 3expa rpcnne0
    resslogrn sseli syl2an ) ACDZAEFZGBHIZDZAHJBKBLJAKMZANDBODUBUCUEUFABPQAROUD
    BSTUA $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( The natural logarithm of ` 1 ` .  One case of Property 1a of [Cohen]
     p. 301.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  log1 $p |- ( log ` 1 ) = 0 $=
    ( c1 clog cfv cc0 wceq ce ef0 crp wcel cr wb 1rp 0re relogeftb mp2an mpbir
    ) ABCDEZDFCAEZGAHIDJIQRKLMADNOP $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( The natural logarithm of ` _e ` .  One case of Property 1b of [Cohen]
     p. 301.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  loge $p |- ( log ` _e ) = 1 $=
    ( ceu clog cfv c1 wceq ce df-e eqcomi crp wcel cr wb ere cc0 clt 1re
    efgt0i breqtrri elrpii relogeftb mp2an mpbir ) ABCDEZDFCZAEZAUDGHAIJDKJUCUE
    LAMNUDAODPQGRSPADTUAUB $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( Relationship between ` pi ` and the natural logarithm function.
     (Contributed by Paul Chapman, 21-Apr-2008.) $)
  pilog $p |- pi = ( _i x. ( log ` -u 1 ) ) $=
    ( vx ci c1 cneg clog cfv cmul co cpi wceq ce c2 caddc cc wcel cz axicn
    pire recni mulcli 1z znegcl ax-mp efper mp2an cmin 2cn negsubi zcn mulcomi
    mulm1i eqtri opreq2i subdii 3eqtr4i negsubdi2i 2timesi eqcomi subaddrii
    negeqi eqtr3i mulneg2i 3eqtri fveq2i cc0 df-neg eulerid 0cn ax1cn efcl
    subadd2i mpbir eqtr2i 3eqtr3i wne crn wb ax1ne0 negne0i cv cim cico crab
    fveq2 eleq1d elrab negcli imnegi addid2i 0re crimi cr cle wbr clt renegcli
    leidi pipos lt0neg2 mpbi lttri w3a elico2 biimpri mp3an eqeltri mpbir2an
    logrn eleqtrri logeftb ixi opreq1i mulassi negnegi ) BCDZEFZGHBBIGHZDZGHZIX
    PXRBGXPXRJZXRKFZXOJZXQBLIGHZGHZXOGHZMHZKFZXQKFZYAXOXQNOZXOPOZYGYHJBIQIRSZTZ
    CPOYJUACUBUCZXQXOUDUEYFXRKYFBIYCUFHZGHZBIDZGHXRXQYDDZMHXQYDUFHYFYOXQYDYLBYC
    QLIUGYKTZTZUHYEYQXQMYEXOYDGHYQYDXOYSYJXONOZYMXOUIUCZUJYDYSUKULUMBIYCQYKYRUN
    UOYNYPBGYCIUFHZDYNYPYCIYRYKUPUUBIYCIIYRYKYKYCIIMHIYKUQURUSUTVAUMBIQYKVBVCVD
    XOVECUFHZYHCVFUUCYHJYHCMHVEJVGVECYHVHVIYIYHNOYLXQVJUCVKVLVMVNYTXOVEVOXREVPZ
    OXTYBVQUUACVIVRVSXRAVTZWAFZYPIWBHZOZANWCZUUDXRUUIOXRNOXRWAFZUUGOZUUHUUKAXRN
    UUEXRJUUFUUJUUGUUEXRWAWDWEWFXQYLWGUUJYPUUGUUJXQWAFZDYPXQYLWHUULIVEXQMHZWAFU
    ULIUUMXQWAXQYLWIVDVEIWJRWKVAUTULYPWLOZYPYPWMWNZYPIWOWNZYPUUGOZIRWPZYPUURWQY
    PVEWOWNZVEIWOWNZUUPUUTUUSWRIWLOZUUTUUSVQRIWSUCWTWRYPVEIUURWJRXAUEUUQUUNUUOU
    UPXBZUUNUVAUUQUVBVQUURRYPIYPXCUEXDXEXFXGAXHXIXOXRXJXEVLUMXSBXQGHZDYPDIBXQQY
    LVBUVCYPBBGHZIGHXOIGHUVCYPUVDXOIGXKXLBBIQQYKXMIYKUKVNUTIYKXNVCVM $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  ${
    relogoprlem.1 $e |- ( ( ( log ` A ) e. CC /\ ( log ` B ) e. CC ) ->
                        ( exp ` ( ( log ` A ) F ( log ` B ) ) ) =
                        ( ( exp ` ( log ` A ) ) G ( exp ` ( log ` B ) ) ) ) $.
    relogoprlem.2 $e |- ( ( ( log ` A ) e. RR /\ ( log ` B ) e. RR ) ->
                          ( ( log ` A ) F ( log ` B ) ) e. RR ) $.
    $( Lemma for ~ relogmul and ~ relogdiv .  Remark of [Cohen] p. 301 ("The
       proof of Property 3 is quite similar to the proof given for Property
       2"). $)
    relogoprlem $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
        ( log ` ( A G B ) ) = ( ( log ` A ) F ( log ` B ) ) ) $=
      ( crp wcel wa clog cfv ce co reeflog opreqan12d fveq2d cr wceq cc recn
      syl2an relogef syl eqtr3d relogcl ) AGHZBGHZIZAJKZLKZBJKZLKZDMZJKZABDMZJK
      UIUKCMZUHUMUOJUFUGUJAULBDANBNOPUIQHZUKQHZUNUPRUFUGUQURIZUPLKZJKZUNUPUISHZ
      UKSHZVAUNRUQURVBVCIUTUMJEPUITUKTUAUSUPQHVAUPRFUPUBUCUDAUEBUEUAUD $.
      $( [21-Apr-2008] $) $( [25-Nov-2007] $)
  $}

  $( The natural logarithm of the product of two positive real numbers is the
     sum of natural logarithms.  Property 2 of [Cohen] p. 301, restricted to
     natural logarithms.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogmul $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( log ` ( A x. B ) ) = ( ( log ` A ) + ( log ` B ) ) ) $=
    ( caddc cmul clog cfv efadd readdcl relogoprlem ) ABCDAEFZBEFZGJKHI $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( The natural logarithm of the quotient of two positive real numbers is the
     difference of natural logarithms.  Exercise 72(a) and Property 3 of
     [Cohen] p. 301, restricted to natural logarithms.  (Contributed by Steve
     Rodriguez, 25-Nov-2007.) $)
  relogdiv $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( log ` ( A / B ) ) = ( ( log ` A ) - ( log ` B ) ) ) $=
    ( cmin cdiv clog cfv efsub resubcl relogoprlem ) ABCDAEFZBEFZGJKHI $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( Exponentiation of a nonzero complex number to a nonnegative integer
     power.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  explog $p |- ( ( A e. CC /\ A =/= 0 /\ N e. NN0 ) ->
                  ( A ^ N ) = ( exp ` ( N x. ( log ` A ) ) ) ) $=
    ( vx cc wcel cc0 wne cn0 w3a clog cfv cmul co ce cexp wceq wa efexp crn
    logcl cv cim cpi cneg cico crab logrn ssrab2 eqsstri sseli syl sylan 3impa
    eflog opreq1d 3adant3 eqtr2d ) ADEZAFGZBHEZIBAJKZLMNKZVANKZBOMZABOMZURUSUTV
    BVDPZVADEZUTVFURUSQZVABRVHVAJSZEVGATVIDVAVICUAUBKUCUDUCUEMEZCDUFDCUGVJCDUHU
    IUJUKULUMURUSVDVEPUTVHVCABOAUNUOUPUQ $.
    $( [21-Apr-2008] $) $( [21-Apr-2008] $)

  $( Exponentiation of a positive real number to a nonnegative integer power.
     (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  reexplog $p |- ( ( A e. RR+ /\ N e. NN0 ) ->
                    ( A ^ N ) = ( exp ` ( N x. ( log ` A ) ) ) ) $=
    ( crp wcel cn0 wa clog cfv cmul co ce cexp cc wceq efexp relogcl recnd
    sylan reeflog opreq1d adantr eqtr2d ) ACDZBEDZFBAGHZIJKHZUEKHZBLJZABLJZUEMD
    UDUFUHNUCUEBOUCUEAPQRUCUHUINUDUCUGABLASTUAUB $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( The natural logarithm of positive ` A ` raised to an nonnegative integer
     power.  Property 4 of [Cohen] p. 301-302, restricted to natural logarithms
     and nonnegative-integer powers ` N ` .  (Contributed by Steve Rodriguez,
     25-Nov-2007.) $)
  relogexp $p |- ( ( A e. RR+ /\ N e. NN0 ) ->
                    ( log ` ( A ^ N ) ) = ( N x. ( log ` A ) ) ) $=
    ( crp wcel cn0 wa clog cfv cmul co ce cexp cc wceq efexp relogcl recnd
    sylan reeflog opreq1d adantr eqtrd fveq2d cr remulcl ancoms nn0re syl2an
    relogef syl eqtr3d ) ACDZBEDZFZBAGHZIJZKHZGHZABLJZGHUPUNUQUSGUNUQUOKHZBLJZU
    SUOMDUMUQVANULUOBOULUOAPZQRULVAUSNUMULUTABLASTUAUBUCUNUPUDDZURUPNUOUDDZBUDD
    ZVCULUMVEVDVCBUOUEUFVBBUGUHUPUIUJUK $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  $( The natural logarithm function on positive reals determines an isomorphism
     from the positive reals onto the reals.  (Contributed by Steve Rodriguez,
     25-Nov-2007.) $)
  relogiso $p |- ( log |` RR+ ) Isom < , < ( RR+ , RR ) $=
    ( crp cr clt clog cres wiso ce ccnv reefiso isocnv ax-mp wceq wb dfrelog
    isoeq1 mpbir ) ABCCDAEZFZABCCGBEZHZFZBACCSFUAIBACCSJKQTLRUAMNABCCTQOKP $.
    $( [21-Apr-2008] $) $( [25-Nov-2007] $)

  ${
    $d A x y $.  $d B y $.
    $( The natural logarithm function on positive reals is strictly monotonic.
       (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
    logltb $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( A < B <-> ( log ` A ) < ( log ` B ) ) ) $=
      ( vx vy crp wcel wa clt wbr clog cres cfv cv wb wral cr wf1o wiso
      relogiso df-iso mpbi pm3.27i wceq breq1 fveq2 breq1d bibi12d breq2
      breq2d rcla42v mpi fvres breqan12d bitrd ) AEFZBEFZGZABHIZAJEKZLZBUSLZHIZ
      AJLZBJLZHIUQCMZDMZHIZVEUSLZVFUSLZHIZNZDEOCEOZURVBNZEPUSQZVLEPHHUSRVNVLGSC
      DEPHHUSTUAUBVKVMAVFHIZUTVIHIZNCDABEEVEAUCZVGVOVJVPVEAVFHUDVQVHUTVIHVEAUSU
      EUFUGVFBUCZVOURVPVBVFBAHUHVRVIVAUTHVFBUSUEUIUGUJUKUOUPUTVCVAVDHAEJULBEJUL
      UMUN $.
      $( [21-Apr-2008] $) $( [25-Nov-2007] $)
  $}


