$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             Specific prime numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( 4 is not a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.)
     (The proof was shortened by Mario Carneiro, 18-Feb-2014.) $)
  4nprm $p |- -. 4 e. Prime $=
    ( c2 c4 2nn 1lt2 cmul co 2t2e4 eqcomi nprmi ) AABCCDDAAEFBGHI $.
    $( [18-Feb-2014] $) $( [22-Jun-2011] $)

  $( Two ways to write the number 25.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  5t5e25 $p |- ( 5 x. 5 ) = ( ( 4 x. 6 ) + 1 ) $=
    ( c4 c6 cmul co c1 caddc c5 5nn nnnn0i 1nn0 dec5 df-6 recni mulid2i opreq1i
    5re eqtr4i decmul1c eqcomi ) ABCDEFDGGCDEEBEGEGGHIJJKJJBGEFDEGCDZEFDLTGEFGG
    PMNZOQAECDEFDGTKUAQRS $.
    $( [18-Feb-2014] $)

  ${
    $d N x $.
    prmlem0.1 $e |- ( x e. ( ZZ>= ` M ) ->
                      ( ( x e. Prime /\ ( x ^ 2 ) <_ N ) -> -. x || N ) ) $.
    prmlem0.2 $e |- ( K e. Prime -> -. K || N ) $.
    prmlem0.3 $e |- M = ( K + 1 ) $.
    $( Lemma for ~ prmlem1 and ~ prmlem2 . $)
    prmlem0 $p |- ( x e. ( ZZ>= ` K ) ->
                    ( ( x e. Prime /\ ( x ^ 2 ) <_ N ) -> -. x || N ) ) $=
      ( cv cuz cfv wcel wceq c1 caddc co cprime wbr cdivides wn wi wo c2 cle wa
      cexp uzp1 eleq1 breq1 notbid imbi12d mpbiri eqcomi fveq2i eleq2s jaoi syl
      adantrd ) AHZBIJKURBLZURBMNOZIJZKZUAURPKZURUBUEODUCQZUDURDRQZSZTZBURUFUSV
      GVBUSVCVFVDUSVCVFTBPKZBDRQZSZTFUSVCVHVFVJURBPUGUSVEVIURBDRUHUIUJUKUQVGURC
      IJVAEUTCICUTGULUMUNUOUP $.
      $( [18-Feb-2014] $)
  $}

  ${
    $d N x $.
    prmlem1.n $e |- N e. NN $.
    prmlem1.lt $e |- N < ( ( 4 x. 6 ) + 1 ) $.
    prmlem1.gt $e |- 1 < N $.
    prmlem1.2 $e |- -. 2 || N $.
    prmlem1.3 $e |- -. 3 || N $.
    $( A quick proof skeleton to show that the numbers less than 25 (= 121 in
       base 4) are prime, by trial division.  (Contributed by Mario Carneiro,
       18-Feb-2014.) $)
    prmlem1 $p |- N e. Prime $=
      ( vx cprime wcel c2 co cle wbr cdivides wn clt c3 c4 c5 cr cuz cv cexp wi
      cfv wral isprm5 cn eluz2b2 mpbir2an prmuz2 eluzelre resqcl syl eluzle cc0
      c1 wa 5re 5nn nnnn0i nn0ge0i le2sq2 mpanl12 syl11anc nnrei resqcli w3a c6
      cmul caddc recni sqvali 5t5e25 eqtr2i breqtri mpani mp3an12 sylc wb ltnle
      ltletr sylancr mpbid pm2.21d adantld 4nprm pm2.21i df-5 prmlem0 df-4 df-3
      a1i exp3a mpcom rgen ) AHIAJUAUEZIZGUBZJUCKZALMZWSANMOZUDZGHUFGAUGWRAUHIU
      QAPMAUIBDUJXCGHWSWQIZWSHIZXCWSUKXDXEXAXBGJQAGQRAGRSAWSSUAUEIZXAXBXEXFXAXB
      XFAWTPMZXAOZXFWTTIZSJUCKZWTLMZXGXFWSTIZXISWSULZWSUMUNZXFXLSWSLMZXKXMSWSUO
      STIUPSLMXLXOURXKUSSSUTVAVBSWSVCVDVEATIZXJTIZXIXKXGUDABVFZSUSVGXPXQXIVHAXJ
      PMXKXGARVIVJKUQVKKZXJPCXJSSVJKXSSSUSVLVMVNVOVPAXJWTWBVQVRVSXFXPXIXGXHVTXR
      XNAWTWAWCWDWEWFRHIRANMOWGWHWIWJQANMOQHIFWMWKWJJANMOJHIEWMWLWJWNWOWPUJ $.
      $( [18-Feb-2014] $)
  $}

  $( 5 is a prime number.  (Contributed by Mario Carneiro, 18-Feb-2014.) $)
  5prm $p |- 5 e. Prime $=
    ( c5 5nn c4 c1 cmul co caddc c6 clt dec5 1nn0 6nn nnnn0i 1lt4 1lt6 cdivides
    decltc c2 wbr c3 eqbrtrri 1lt5 dec2dvds1 breq2i 3nn 2nn 3cn mulid1i opreq1i
    mtbi 3p2e5 eqtr2i 2lt3 ndvdsi prmlem1 ) ABCDEFDGFZACHEFDGFIJDHDDKHLMKKNOQUA
    UBRUPPSRAPSDKUCUPARPJUDUJTADRUEKUFTDEFZRGFTRGFAUQTRGTUGUHUIUKULUMUNUO $.
    $( [18-Feb-2014] $)

  $( 6 is not a prime number.  (Contributed by Mario Carneiro, 18-Feb-2014.) $)
  6nprm $p |- -. 6 e. Prime $=
    ( c3 c2 c6 3nn 2nn 1lt3 1lt2 cmul co 3t2e6 eqcomi nprmi ) ABCDEFGABHICJKL
    $.
    $( [18-Feb-2014] $)

  $( 7 is a prime number.  (Contributed by Mario Carneiro, 18-Feb-2014.) $)
  7prm $p |- 7 e. Prime $=
    ( c7 7nn c4 c1 cmul co c3 caddc clt dec7 1nn0 6nn nnnn0i 3nn0 3lt4 cdivides
    c6 1lt6 c2 wbr decltc eqbrtrri 1lt7 dec2dvds3 breq2i mtbi 3nn 2nn0 1nn df-7
    3t2e6 opreq1i eqtr4i 1lt3 ndvdsi prmlem1 ) ABCDEFGHFZACQEFDHFIJDQGDKQLMNKOR
    UAUBUCSUQPTSAPTDKUDUQASPJUEUFGASDUGUHUIAQDHFGSEFZDHFUJURQDHUKULUMUNUOUP $.
    $( [18-Feb-2014] $)

  $( 8 is not a prime number.  (Contributed by Mario Carneiro, 18-Feb-2014.) $)
  8nprm $p |- -. 8 e. Prime $=
    ( c4 c2 c8 4nn 2nn 1lt4 1lt2 cmul co 4t2e8 eqcomi nprmi ) ABCDEFGABHICJKL
    $.
    $( [18-Feb-2014] $)

  $( 9 is not a prime number.  (Contributed by Mario Carneiro, 18-Feb-2014.) $)
  9nprm $p |- -. 9 e. Prime $=
    ( c3 c9 3nn 1lt3 cmul co 3t3e9 eqcomi nprmi ) AABCCDDAAEFBGHI $.
    $( [18-Feb-2014] $)

  $( 10 is not a prime number.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  10nprm $p |- -. 10 e. Prime $=
    ( c5 c2 c10 5nn 2nn 1lt5 1lt2 cmul co 5t2e10 eqcomi nprmi ) ABCDEFGABHICJKL
    $.
    $( [18-Feb-2014] $)

  $( 11 = 23 base 4 is a prime number.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  11prm $p |- ( ( 4 x. 2 ) + 3 ) e. Prime $=
    ( c4 c2 cmul co c3 caddc 2nn0 3nn decnnclc c6 c1 nnnn0i 3nn0 1nn0 3lt4 2lt6
    6nn decltc 2nn 1lt4 declti dec2dvds3 3t3p2 2lt3 ndvdsi prmlem1 ) ABCDEFDZBE
    GHIBJEKGJQLMNOPRBEKSMNTUABGUBEUGEBHMSUCUDUEUF $.
    $( [18-Feb-2014] $)

  $( 13 = 31 base 4 is a prime number.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  13prm $p |- ( ( 4 x. 3 ) + 1 ) e. Prime $=
    ( c4 c3 cmul co c1 caddc 3nn0 1nn decnnclc 6nn nnnn0i 1nn0 1lt4 3lt6 decltc
    c6 3nn declti dec2dvds1 4nn0 nn0cni 3cn mulcomi opreq1i 1lt3 ndvdsi prmlem1
    ) ABCDZEFDZBEGHIBPEEGPJKLLMNOBEEQLLMRBGSBUIAEQTHUHBACDEFABATUAUBUCUDUEUFUG
    $.
    $( [18-Feb-2014] $)

  $( 17 = 101 base 4 is a prime number.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  17prm $p |- ( ( 4 x. 4 ) + 1 ) e. Prime $=
    ( c4 cmul co c1 caddc 4nn0 1nn decnnclc c6 6nn nnnn0i 1nn0 1lt4 4lt6 c3 cc0
    c5 c2 2nn0 dec5 decltc 4nn declti dec2dvds1 3nn 5nn 2nn 0nn0 dec0h 3nn0 3cn
    df-4 mulid1i ax-1cn addid2i opreq12i eqtr4i opreq1i 3eqtr4ri decma2c ndvdsi
    3p2e5 2lt3 prmlem1 ) AABCDECZADFGHAIDDFIJKLLMNUAADDUBLLMUCAFUDOVEQRUEQUFKUG
    DDPROADDQRLLUHSTRSUIUJLLAODECODBCZPDECZECULVFOVGDEOUKUMZDUNUOUPUQORECQVFREC
    ADBCDECVBVFOREVHURTUSUTVCVAVD $.
    $( [18-Feb-2014] $)

  $( 19 = 103 base 4 is a prime number.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  19prm $p |- ( ( 4 x. 4 ) + 3 ) e. Prime $=
    ( c4 cmul co c3 caddc 4nn0 3nn decnnclc c6 c1 nnnn0i 3nn0 1nn0 3lt4 4lt6 c2
    6nn 2nn0 mulid1i opreq1i decltc 4nn 1lt4 declti dec2dvds3 1nn df-3 dec6 3cn
    df-4 eqtr4i 4p2e6 nn0cni 3t2e6 3eqtr4i decmul2c decsuc 1lt3 ndvdsi prmlem1
    ) AABCDECZADFGHAIDJFIQKZLMNOUAADJUBLMUCUDAFUEDVAIJGVBUFAPDDIBCFRUGJPAPDJILM
    RUHRMADJECDJBCZJECUJVCDJEDUISTUKAPECIAJBCZPECDPBCULVDAPEAAFUMSTUNUOUPUQURUS
    UT $.
    $( [18-Feb-2014] $)

  $( 23 = 113 base 4 is a prime number.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  23prm $p |- ( ( 4 x. 5 ) + 3 ) e. Prime $=
    ( c4 c5 cmul co c3 caddc 5nn nnnn0i 3nn decnnclc c6 c1 3nn0 1nn0 3lt4 c7 c2
    6nn cc0 2nn0 5lt6 decltc 1lt4 dec2dvds3 7nn 2nn 0nn0 dec7 dec0h 3cn mulid1i
    declti 2cn addid2i opreq12i 3p2e5 eqtr2i 3t3p2 decma2c 2lt3 ndvdsi prmlem1
    ) ABCDEFDZBEBGHZIJBKELVDKRHMNOUAUBBELGMNUCULBVDUDEVCPQIPUEHUFLESQEBEQPQNMUG
    TUHQTUIMMTELCDZSQFDZFDEQFDBVEEVFQFEUJUKQUMUNUOUPUQURUSUTVAVB $.
    $( [18-Feb-2014] $)

  ${
    $d x N $.
    prmlem2.n $e |- N e. NN $.
    prmlem2.lt $e |- N < ( ( 4 x. ( ( 4 x. ( 4 x. ( ( 4 x.
                          3 ) + 1 ) ) ) + 2 ) ) + 1 ) $.
    prmlem2.gt $e |- 1 < N $.
    prmlem2.2 $e |- -. 2 || N $.
    prmlem2.3 $e |- -. 3 || N $.
    prmlem2.5 $e |- -. 5 || N $.
    prmlem2.7 $e |- -. 7 || N $.
    prmlem2.11 $e |- -. ( ( 4 x. 2 ) + 3 ) || N $.
    prmlem2.13 $e |- -. ( ( 4 x. 3 ) + 1 ) || N $.
    prmlem2.17 $e |- -. ( ( 4 x. 4 ) + 1 ) || N $.
    prmlem2.19 $e |- -. ( ( 4 x. 4 ) + 3 ) || N $.
    prmlem2.23 $e |- -. ( ( 4 x. 5 ) + 3 ) || N $.
    $( Our last proving session got as far as 25 because we started with the
       two "bootstrap" primes 2 and 3, and the next prime is 5, so knowing that
       2 and 3 are prime and 4 is not allows us to cover the numbers less than
       ` 5 ^ 2 = 2 5 = 4 x. 6 + 1 ` .  Additionally, nonprimes are "easy", so
       we can extend this range of known prime/nonprimes all the way until 29,
       which is the first prime larger than 25.  Thus in this lemma we extend
       another blanket out to ` 2 9 ^ 2 = 8 4 1 = 3 1 0 2 1 ` (base 4), from
       which we can prove even more primes.  If we wanted, we could keep doing
       this, but the goal is Bertrand's postulate, and for that we only need a
       few large primes - we don't need to find them all, as we have been doing
       thus far.  So after this blanket runs out we'll have to switch to
       another method (see ~ 1259prm ).

       As a side note, you can see the pattern of the primes in the indentation
       pattern of this lemma!  (Contributed by Mario Carneiro, 18-Feb-2014.) $)
    prmlem2 $p |- N e. Prime $=
      ( wcel c2 co wbr c1 c3 caddc vx cprime cuz cfv cv cexp cle cdivides wn wi
      wral isprm5 cn clt eluz2b2 mpbir2an prmuz2 c4 c5 c6 c7 c8 c9 c10 eluzelre
      cmul cr resqcl syl eluzle cc0 wa nnnn0i 1nn0 decclc nn0rei nn0ge0i le2sq2
      7nn mpanl12 syl11anc nnrei resqcli w3a eqid 3nn0 dec7 2nn0 dec0u 0nn0 2cn
      dec0h df-3 ax-1cn addcomi 3eqtri 6nn nncni mulid2i eqtr4i addid2i opreq1i
      dec6 df-2 eqtri 3p2e5 df-5 eqtr2i decaddc 3cn mulid1i 3p3e6 3t3p3 decma2c
      opreq12i decmac eqtr3i 4t2e8 decmul2c 4nn 1lt4 1lt7 nprmi pm2.21i prmlem0
      eqcomi 3nn 1lt3 addid1i df-7 decsucc dec2nprm 5nn 1lt5 a1i df-6 df-9 4nn0
      decsuc df-4 8p2e10 7p3e10 3eqtr4i nn0cni sqvali breqtri ltletr mpani sylc
      mp3an12 wb ltnle sylancr mpbid pm2.21d adantld 9nn 1lt9 dec9 3t2e6 5t5e25
      1lt6 3t3e9 3eqtr4ri dec5 10nprm dec10 9nprm df-10 8nprm 6nprm 4nprm exp3a
      df-8 mpcom rgen ) AUBNAOUCUDZNZUAUEZOUFPZAUGQZUVSAUHQUIZUJZUAUBUKUAAULUVR
      AUMNRAUNQAUOBDUPUWCUAUBUVSUVQNZUVSUBNZUWCUVSUQUWDUWEUWAUWBUAOSAUASURAUAUR
      USAUAUSUTAUAUTVAAUAVAVBAUAVBVCAUAVCVDAUAVDUROVFPZSTPZAUAUWGURSVFPZAUAUWHU
      WHRTPZAUAUWIUWHOTPZAUAUWJUWHSTPZAUAUWKURURVFPZAUAUWLUWLRTPZAUAUWMUWLOTPZA
      UAUWNUWLSTPZAUAUWOURUSVFPZAUAUWPUWPRTPZAUAUWQUWPOTPZAUAUWRUWPSTPZAUAUWSUR
      UTVFPZAUAUWTUWTRTPZAUAUXAUWTOTPZAUAUXBUWTSTPZAUAUXCURVAVFPZAUAUXDUXDRTPZA
      UVSUXEUCUDNZUWAUWBUWEUXFUWAUWBUXFAUVTUNQZUWAUIZUXFUVTVGNZUXEOUFPZUVTUGQZU
      XGUXFUVSVGNZUXIUXEUVSVEZUVSVHVIZUXFUXLUXEUVSUGQZUXKUXMUXEUVSVJUXEVGNVKUXE
      UGQUXLUXOVLUXKUXEVARVAVSVMZVNVOZVPZUXEUXQVQUXEUVSVRVTWAAVGNZUXJVGNZUXIUXK
      UXGUJABWBZUXEUXRWCUXSUXTUXIWDAUXJUNQUXKUXGAURURURUWIVFPZVFPOTPZVFPRTPZUXJ
      UNCUYDUXEUXEVFPUXJVARUYCRUXEVAUXEUXQUXPVNUXEWEZVNUXPVARRSVAUYBOOUXEVAUXPV
      NVNWFUYEWGUXPWHWHUYBVKTPUYBVAVAVFPROTPZTPUWISRWFVNVOWIRSVKSVAUWIVKUTVAUYF
      VNWFWJWFWGURVKVFPSTPZSORTPZUYFSWFWLZWMORWKWNWOWPUXPWJUTWQVMZRSROSRRVAVFPZ
      VKUTTPZVNWFVNWHURRVFPZSTPVAUYKWGVAVAVSWRWSZWTUYMOTPUTUYLXCUTUTWQWRZXAWTVN
      SUYHRRTPZRTPWMOUYPRTXDXBXESOTPZUSURRTPXFXGXHXIRSVKSSUTVKSVASVNWFWJWFWGUYI
      WFWJWFSRVFPZVKSTPZTPSSTPUTUYRSUYSSTSXJXKZSXJXAXOXLXHUWHVKTPUWHSSVFPZSTPSW
      FWIXMXEXNXPXQVBOTPZVASTPZUWFOTPUYKSTPVUBVDVUCUUAUUBWTUWFVBOTXRXBUYKVASTUY
      NXBUUCXPUXERVFPUXEUXEUXEUXQUUDZXKYFXSUXEVUDUUEWTUUFAUXJUVTUUGUUHUUJUUIUXF
      UXSUXIUXGUXHUUKUYAUXNAUVTUULUUMUUNUUOUUPUXDUBNUXDAUHQUIURVAUXDXTVSYAYBUXD
      WEYCYDUYEYEUXCUBNUXCAUHQUISVCUXCYGUUQYHUURORUTSSVKVCWFWHVNUUSWFWJSOVFPZVK
      TPUTVKTPUTVUEUTVKTUUTXBUTUYOYIXHUYGSUYRUYIUYTWTZXSYCYDUTVAUXCUYJYJUXCWEYK
      YEUXBUBNUXBAUHQUIUTWQYLYDUTOSUXBUYJWHWMUXBWEYSYEUXAUBNUXAAUHQUIUSUSUXAYMY
      MYNYNUSUSVFPUXAUVAYFYCYDUTROUXAUYJVNXDUXAWEZYSYEUWTUBNUWTAUHQUIURUTUWTXTW
      QYAUVBUWTWEYCYDVUGYEUWSAUHQUIUWSUBNMYOUSUTUWSUSYMVMZYPUWSWEYKYEUWRUBNUWRA
      UHQUIUSYMYLYDUSOSUWRVUHWHWMUWRWEYSYEUWQUBNUWQAUHQUISVAUWQYGVSYHYBRSUSRSOV
      AWFVNWFWGVNWHUYROTPUYQUSUYRSOTUYTXBXFXHVCVBRTPVUAUWFRTPYQUVCUWFVBRTXRXBUV
      DXSYCYDUSROUWQVUHVNXDUWQWEZYSYEUWPUBNUWPAUHQUIURUSUWPXTYMYAYNUWPWEYCYDVUI
      YEUWOAUHQUIUWOUBNLYOURUSUWOYRXGUWOWEYKYEUWNUBNUWNAUHQUIURXTYLYDUROSUWNYRW
      HWMUWNWEYSYEUWMAUHQUIUWMUBNKYOURROUWMYRVNXDUWMWEZYSYEUWLUBNUWLAUHQUIURURU
      WLXTXTYAYAUWLWEYCYDVUJYEUWKUBNUWKAUHQUISUSUWKYGYMYHYNRRSSSVKUSWFVNVNUVEWF
      WJUYRVKTPSVKTPSUYRSVKTUYTXBSXJYIXHVUFXSYCYDSURUWKWFYTUWKWEYKYEUWJUBNUWJAU
      HQUISYGYLYDSOSUWJWFWHWMUWJWEYSYEUWIAUHQUIUWIUBNJYOSROUWIWFVNXDUWIWEZYSYEU
      WHUBNUWHAUHQUIURSUWHXTYGYAYHUWHWEYCYDVUKYEUWGAUHQUIUWGUBNIYOOSUWGWHWMUWGW
      EYKYEVDUBNVDAUHQUIUVFYDOOSVDWHWHWMUVGYSYEVCUBNVCAUHQUIUVHYDUVIYEVBUBNVBAU
      HQUIUVJYDYQYEVAAUHQUIVAUBNHYOUVNYEUTUBNUTAUHQUIUVKYDYJYEUSAUHQUIUSUBNGYOY
      PYEURUBNURAUHQUIUVLYDXGYESAUHQUISUBNFYOYTYEOAUHQUIOUBNEYOWMYEUVMUVOUVPUP
      $.
      $( [18-Feb-2014] $)
  $}

  $( 37 = 211 base 4 is a prime number.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  37prm $p |- ( ( 4 x. 9 ) + 1 ) e. Prime $=
    ( c4 c9 cmul co c1 caddc decnnclc c3 c2 3nn0 1nn0 2nn0 0nn0 nncni ndvdsi c5
    cc0 c7 4nn0 opreq1i 9nn nnnn0i 1nn decclc deccl 1lt4 dec9 2lt4 declti dec0u
    clt breqtri decltc eqbrtrri dec2dvds1 3nn 0p1e1 decnncl addid1i opreq2i 4nn
    3t3e9 3cn mul12i 3eqtr2i decsuc 1lt3 5nn 7nn 2nn dec7 dec0h mulid1i addid2i
    opreq12i 5p4e9 eqtr2i dec5 df-4 mulid2i ax-1cn eqtr4i 3p2e5 3eqtr4ri decmac
    decma2c 2lt5 mulcomi eqtri 2lt7 eqid dec4 c6 6p3e9 3t2e6 eqcomi df-3 eqtr3i
    2cn addcomi df-9 4t2e8 3eqtr4i 4pos 1lt2 3lt4 2lt3 4p4e8 df-5 decaddc declt
    c8 5p3e8 3lt5 prmlem2 ) ABCDZEFDZBEBUAUBZUCGBAAAHCDZEFDZCDZCDIFDZEEXRYAIXTH
    EJKUDZUEZLUDKKUFAICDZEFDZBYBUKUGIYAEILYDKLUFIYAQFDYAUKXTQIHEJUCGZMLUHUIXTYC
    UJULUMUNUMBEEUAKKUFUIBXRUOHXQXSEUPHJUEUCBQEHXSCDZXRMUQXPQFDXPAHHCDZCDYHXPXP
    BUAURNUSYIBACVBUTAHHAVANZVCVCVDVEVFVGOPXQRIVHRVIUBVJEHQIPBEARIKJMLVKILVLZPV
    HUBZKSPECDZQAFDZFDPAFDBYMPYNAFPPVHNZVMAYJVNVOVPVQEEQIHAEEPIKKMLVRYKJKKAHEFD
    EHCDZQEFDZFDVSYPHYQEFHVCVTZEWAVNZVOWBHIFDZPYPIFDAECDZEFDZWCYPHIFYRTVRWDWEWF
    ZWGORXQPIVIYLVJXQPRCDZIFDRPCDZIFDUUCUUDUUEIFPRYORVINWHTWIWJOYEHFDZXQHAIHLUP
    GZJVAIHEQHBEIUUFALJKMUUFWKZWLJKLBHICDZIEFDZFDZIHCDZEIFDZFDWMHFDBUUKWNWMUUIH
    UUJFUUIWMWOWPWQVOWRZUUIUULUUJUUMFHIVCWSWHIEWSWAWTVOWIXLEFDZBQFDZYFYIQFDUUPB
    UUOBBUANUSXAVQYEXLEFXBTYIBQFVBTXCWEUUAQFDAUUFUKWLEIQHKLMJXDXEUMUNOXTXQIUUFY
    GLUUGHEIHIBEEXTUUFJKLJXTWKUUHLKKUUNPIHFDZUUBEICDZHFDYTPUUQWCHIVCWSWTWRVRUUR
    IHFIWSVTTXCZWEIHHELJJKXFXGUMOAACDZEFDZXQIHAESUCGLUPAEQHIBEEUVAHSKMJUVAWKHJV
    LLKKBUUOYEYQFDXAYEXLYQEFXBYSVOWBUUSWEAEHVAKJXFUIOUUTHFDZXQEUUTIFDZAHSUPGZKA
    ISVJGAHAIBEUVBECDZUVCSJSLUVEUVBUVBUVBUVDNVMWPUVCWKKBUUOAAFDZEFDXAUVFXLEFXHT
    WBYTPAEFDWCXIVQZXJAIHSLUPXGXKOAPCDHFDZXQEXSIFDZPHYLUPGZKHIJVJGPHHIBEUVHECDZ
    UVIYLJJLUVKUVHUVHUVHUVJNVMWPUVIWKKBUUOPHFDZEFDXAUVLXLEFXMTWBUVGXJHPIHJYLLJU
    HXNUMOXO $.
    $( [18-Feb-2014] $)

  $( 43 = 223 base 4 is a prime number.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  43prm $p |- ( ( 4 x. 10 ) + 3 ) e. Prime $=
    ( c4 c10 cmul co c3 caddc 3nn c1 3nn0 1nn0 2nn0 clt cc0 0nn0 opreq1i eqtr4i
    c2 c6 ndvdsi c5 10nn nnnn0i decnnclc decclc deccl 3lt4 dec10 2lt4 1nn dec0u
    declti breqtri decltc eqbrtrri 1lt4 dec2dvds3 df-3 c9 df-10 3t3e9 4p2e6 4nn
    eqid nncni mulid1i 3t2e6 3eqtr4i decmul2c decsuc 1lt3 c8 5nn dec8 nn0mulcli
    8nn nn0cni addid1i 5t2e10 eqtr2i dec0h mul01i eqtr3i 3lt5 7nn 6nn dec7 4nn0
    mulid2i 6p4e10 dec6 df-4 3cn decmul1c 1lt7 2cn mulcomi eqtri 2p2e4 opreq12i
    c7 3t3p2 decmac 2lt3 declt dec4 ax-1cn 3eqtr4ri 4pos dec9 4t2e8 8p2e10 dec5
    9nn 1p1e2 4p3e7 df-7 decnncl 5p5e10 eqcomi decadd 3pos prmlem2 ) ABCDZEFDZB
    EBUAUBZGUCBAAAECDZHFDZCDZCDQFDZEHYEYHQYGEHIJUDZUEZKUDIJUFAQCDZQFDZBYILUGQYH
    QQKYKKKUHQYHMFDYHLYGMQEHIUIUCZNKUHUKYGYJUJULUMUNUMBEHUAIJUOUKBYEUPEYDYFQFDZ
    HGEQIKUDUIBQEEYOCDYEKUQEQBQEHYOIIKYOVCKJBURHFDZEECDZHFDUSYQURHFUTOPAQFDRAHC
    DZQFDZEQCDZVAYRAQFAAVBVDVEZOVFVGVHVIVJSTYDVKEVLVKVOUBGYCTVKCDZEFYCMFDYCUUBB
    YEUJQMBMTMVKTVLUBZKNVMNNTQCDZMFDUUDBUUDUUDTQUUCKVNVPVQVRVSAMCDZMFDMTMCDMNVT
    TTVLVDWAPVHWBOWCSWTYDRHWDRWEUBZUIBQEWTRCDYEKUQHEBQRAWTUUFJIWFKWGHRCDZAFDRAF
    DZBUUGRAFRRWEVDWHOWIVSHQAQEHRIJKWJKJAEHFDEHCDZHFDWKUUIEHFEWLVEOPYSRYTWJVFPV
    HWMVIWNSYLEFDZYDEBQEKGUCIUAQEQQEBEQUUJBKIKKUUJVCUGIIKQECDZQQFDZFDUUHBUUKRUU
    LAFUUKYTRQEWOWLWPVFWQWRWSWIVSXAXBYMBUUJLUGQQEKKGXCXDUNSYGYDEAYNIVBEHHMEBEMY
    GAIJJNYGVCXEIINBYPYQHMFDZFDUSYQURUUMHFUTHXFVQWSPEMFDZEHECDZMFDUUEEFDZEWLVQZ
    UUOEMFEWLWHOEIVTZXGXBYRMFDAYGLXEHEMHJINJXHVJUMUNSAACDZHFDZYDQURAHWGUIUCKXMA
    HQHQBEMUUTURWGJKJUUTVCXIKINYLQMFDZFDVKQFDZBYLVKUVAQFXJQWOVQWSXKVSEQHFDUUPHQ
    CDZHFDUQUURUVCQHFQWOWHOVGXBYLHFDURUUTLXIQAHHKWGJJUOUHUMUNSUUSEFDZYDQTAEWGGU
    CKVLAEHHQBEHUVDTWGIJJUVDVCXLKIJYLHHFDZFDUVBBYLVKUVEQFXJXNWSXKVSAEFDWTYREFDY
    THFDZXOYRAEFUUAOUVFRHFDWTYTRHFVFOXPPVGXBYRHFDTUVDLXLHAHEJWGJIUOUOUMUNSATCDZ
    EFDZYDHUVGTEUUCGUCZJTVLXQYDUVHUVGFDUVHHCDZUVGFDTETMBEUVHUVGUUCIUUCNUVHVCTUU
    CUJZTTFDBXRXSUUNEUUQXSXTUVJUVHUVGFUVHUVHUVIVDVEOPUVGMFDUVGUVHLUVKTMEUUCNGYA
    XDUNSYB $.
    $( [18-Feb-2014] $)

  $( 83 = 1103 base 4 is a prime number. $)
  83prm $p |- ( ( 4 x. ( 4 x. 5 ) ) + 3 ) e. Prime $=
    ( c4 c5 cmul co c3 caddc 3nn decnnclc c1 c2 3nn0 1nn0 2nn0 cc0 0nn0 opreq1i
    decltc c6 4nn0 c7 5nn nnnn0i deccl decclc 3lt4 clt 4pos 1lt4 declti 3brtr3i
    dec0u dec5 eqbrtrri decnncl dec2dvds3 6nn 2nn eqid dec0h 2cn addid2i eqtr4i
    dec6 3cn mulid1i opreq12i 3p2e5 eqtr2i c8 6p2e8 3t2e6 dec8 3eqtr4ri decma2c
    eqtr3i 3t3p2 2lt3 ndvdsi 4nn nncni mulcomi opreq2i eqtri 3lt5 7nn dec7 df-6
    mul12i ax-1cn addcomi 3eqtr4i mulid2i df-3 3eqtrri 6lt7 2lt4 1lt2 1nn 4p3e7
    decmac df-2 df-7 1lt3 dec4 df-4 df-5 addid1i 0cn 1p1e2 prmlem2 ) AABCDZCDZE
    FDZXKEBBUAUBZUCZGHXKAAAECDZIFDZCDZCDJFDZEIXOXRJXQEIKLUDZUCZMUDKLUEXKNFDZXKX
    SUFBXNUKZBXRNJXNYAOMUGAICDZIFDZXRNFDBXRUFIXQINLXTLOUHEIIGLLUHUIQULXQXTUKUJQ
    UMQXKEIBUAUNKLUHUIXKXOUOEXMARCDEFDZJGRERUPUBZKUDUQRENJEXKEJYFJYGKOMYFURJMUS
    ZKKMYBXKERCDZNJFDZFDZYCIJNJEBNJRYJLMOMVCANCDZJFDJYJYHJUTVAZVBKOMEICDZYJFDEJ
    FDZBYNEYJJFEVDVEYMVFVGVHRJFDVIEJCDZJFDAJCDZNFDVJYPRJFVKPVLVMZVNVOZVPVNVQVRB
    XMAACDZEUAASUCGXLBYTCDZEFXLABACDZCDUUAXKUUBACABAVSVTZBUAVTZWAWBABAUUCUUDUUC
    WHWCPWDVRTXMYQEFDZRWEJEMKUDUPJEIJTXKEBUUERMKLMUUEURVCTWEUBZKXNYBXKTJCDIBFDZ
    FDYCIEIJJBNJTUUGLKLMWFRBIFDYDJFDZUUGWGVCIBWIUUDWJWKMOMIJCDZIJFDZFDJEFDYOBUU
    IJUUJEFJUTWLUUJJIFDEIJWIUTWJWMVBVFJEUTVDWJVGWNYRWTVOIENJEBEJTJLKOMWFYHKKMIE
    CDZYJFDYOBUUKEYJJFEVDWLZYMVFVGVHVPWTVNZWOVRUUEXMTRJEMGHZUUFUPXMTUUECDZRFDUU
    ETCDZRFDUUMUUOUUPRFTUUETWEVTUUEUUNVTWAPWCUUHRUUEUFVCIJJELMMKWPWQQUMVRXQXMRB
    EIKWRHYGUAEIIIRXKEIXQBKLLLXQURULYGKLXKYKYIIIFDZFDYSYJUUQYIFYJJUUQYMXAWCWBWC
    AEFDZRIFDZYDEFDZIRCDZIFDUURTUUSWSXBWCYDAEFAUUCVEPUVARIFRRUPVTWLPWKWTYEBXQUF
    ULIEIILKLLUHXCQUMVRYTIFDZXMAXPEFDZAISWRHSEEKGHAIEEAXKEIUVBUVCSLKKUVBURUVCUR
    SKLYBXKYTEIFDZFDZYCININABNNAUVDLOLOXDYDNFDZAUVDXDXEWCSOOBAIFDIACDZINFDZFDXF
    UVGAUVHIFAUUCWLIWIXGVFVBYLNACDNFANUUCXHWAPWTVOZYDUVGEFAIUUCWIWAPWTEAEIKSKLU
    EUEQVRYTEFDZXMATAESGHSWEAEIEAXKEEUVJTSKLKUVJURWFSKKXKUVEYTIEFDZFDUVIUVDUVKY
    TFEIVDWIWJWBWCXPEACDEFAEUUCVDWAPWTUUTTUVJUFWFIAEELSKKUEUHQUMVRXKEFDZXMEXPJF
    DZBEXNGHKEJKUQHBEEJEXKEJUVLUVMXNKKMUVLURUVMURKKMYBXKBECDYOFDYCIIIIEBNIBYOLL
    LLULYEBYOULVGVBKOLUUKUUQFDYOBUUKEUUQJFUULXIVFVGVHAUVDUVFUUKIFDXEXDUUKEIFUUL
    PWKWTVOVPWTEBJEKXNMKWPWDQVRXJ $.
    $( [18-Feb-2014] $)

  $( 139 = 2033 base 4 is a prime number.  (Contributed by Mario Carneiro,
     19-Feb-2014.) $)
  139prm $p |- ( ( 4 x. ( ( 4 x. 8 ) + 2 ) ) + 3 ) e. Prime $=
    ( c4 c8 cmul co c2 caddc c3 2nn0 c1 3nn0 1nn0 cc0 opreq1i c6 c10 decma2c c7
    0nn0 eqtr4i c5 8nn nnnn0i decclc 3nn decnnclc deccl 3lt4 2lt4 declti decltc
    clt 4pos dec8 dec0u 3brtr3i 2nn 1lt4 dec2dvds3 1nn dec0h nncni mul01i 3t2e6
    eqid 4nn 2cn addid2i opreq12i 6p2e8 eqtr2i 8p2e10 df-10 eqtri 4t2e8 3eqtr4i
    c9 3t3e9 mulid1i df-7 1lt3 ndvdsi 5nn 6nn dec4 dec6 df-4 ax-1cn 3cn addcomi
    4p3e7 df-3 5p3e8 10nn addid1i dec10 3eqtr4ri dec5 mulid2i decmul1c nnmulcli
    5t2e10 4lt5 7nn 4nn0 dec7 df-6 4p4e8 mulcomi 3p2e5 3t3p2 6lt7 1lt2 eqbrtrri
    decmac 9nn dec9 2p2e4 1p1e2 decsuc 2lt3 eqtr3i 4p2e6 decmul2c prmlem2 ) AAB
    CDEFDZCDZGFDZYEGBEBUAUBZHUCZUDUEYEAAAGCDZIFDZCDZCDEFDGIYIYLEYKGIJKUCZUFZHUC
    JKUGBYLEEYHYNHHUHAECDZLFDZYLLFDBYLUKEYKLLHYMRRULGIEUDKHUHUIUJUMYKYMUNUOUJUJ
    YEGIBEYHUPUEJKUQUIYEYIURGYGAYOGFDZCDEFDZIUDYQEEGHJUCZHUCUSYQELIGYEGIYRIYSHR
    KYRVDIKUTZJJKEGLIGBEEYQLIFDHJRKYQVDZALCDZLIFAAVEVAZVBZMJHHGECDZLEFDZFDNEFDZ
    BUUENUUFEFVCEVFVGZVHVIVJBEFDZVPIFDZYOEFDZGGCDZIFDZUUIOUUJVKVLVMYOBEFVNMUULV
    PIFVQMZVOPAGFDQAICDZGFDZUUEIFDZWJUUOAGFAUUCVRZMUUQNIFDQUUENIFVCMVSSVOPVTWAT
    YGANCDGFDZAWBNGNWCUBZJUCVENGILTYEGGUUSAUUTJKRUUSVDWDTWBUBZJJIEILTBEENIGFDZK
    HKRWEAGIFDZUUOLFDZUVBWFWDIGWGWHWIZVOUVAHHTICDZIEFDZFDTGFDBUVFTUVGGFTTWBVAZV
    RUVGEIFDGIEWGVFWIWKSVHWLVJZOLFDOTECDZLFDUUKOOWMVAZWNUVJOLFXAMWOWPZPYJGFDTGC
    DZUVMLFDIIGGGLTJKKWQJRIGCDZLFDGLFDGUVNGLFGWHWRZMGWHWNVJUUBGFDGUVNGJUTUVOSWS
    UVMUVMTGWBUDWTVAWNSPXBWAQYGAACDZGFDZNXCAGXDJUCWCAGIEQYEGTUVQNXDJKHUVQVDZWEQ
    XCUBZJUVAIGIEABEGQITFDZKJKHXENTIFDUUOEFDZUVTXFWEITWGUVHWIVOZXDHJIACDZUVBFDA
    AFDZBUWCAUVBAFAUUCWRUVBUVCAUVEWFSZVHXGVJYJGACDEFAGUUCWHXHMXNIGLEGTGEQEKJRHX
    EEHUTZJJHUVNUUFFDGEFDZTUVNGUUFEFUVOUUHVHXIVJXJXNPXKWAYQYGYJQEGHUDUEZGJUFXCG
    LIGYQYEGLYJQJRKJGJUNXEYSJREGLIGBEEYQILFDZHJRKUUAUUBIFDIUWIYTIWGWNSJHHEGCDZU
    UFFDUUGBUWJNUUFEFUWJUUENEGVFWHXHVCVMUUHVHVIVJOUUJUUKUUMVLWOUUNVOXNUUBYQLCDZ
    GFUUBLUWKUUDYQYQUWHVAVBSMPUUPQYQUKXEIEGGKHJJUGXLUJXMWAYKYGOVPGIJUSUEOWMUBZX
    OGIEIOYEGEYKVPJKHKYKVDXPUWLJHEEILGBEIOEEFDZHHKRWOUVDAUWMWDXQSJHKUUEIIFDZFDU
    UGBUUENUWNEFVCXRVHVIVJNLFDNUUELFDUWANNWCVAWNUUENLFVCMWEWPPEEGIOCDZHHWKUUKOU
    WOWOOUVKWRSXSXNYOIFDVPYKUKXPEGIIHJKKUQXTUJXMWAUVPIFDZYGBGAIXDUSUEYHUDYFUWPB
    CDZGFYFLFDYFUWQYEYIUNAIYELBEUWPYHXDKUWPVDRHYEVDBLFDBYPIBCDBBUAVAZWNYOBLFVNM
    BUWRWRVOWSYAMAIGVEKJUGUIWAUVQYGQNAGXDUDUEUVSWCAGIEQYEGTUVQNXDJKHUVRWEUVSJUV
    AIGIEABEGQUVTKJKHXEUWBXDHJUUOUVBFDUWDBUUOAUVBAFUURUWEVHXGVJYJEFDVDPIGLEGTGE
    QEKJRHXEUWFJJHGICDZUUFFDUWGTUWSGUUFEFGWHVRZUUHVHXIVJXJPXNUWANUVQUKWEIAEGKXD
    HJUHUQUJXMWAATCDGFDZYGNITGUVAUDUEUUTUSYEEGUXANCDYIHWKTGYEENAUXAUUTUVAJUXAVD
    HXDIEILTBEENAKHKRWEWDUVAHHUVIUVLPIEAEGINJKHWEHKAUVCUWSIFDWFUWSGIFUWTMSAEFDN
    UWAUUEYBUUOAEFUURMVCVOYCWSXSTGIWBJKUQUIWAYD $.
    $( [19-Feb-2014] $)

  $( 163 = 2203 base 4 is a prime number.  (Contributed by Mario Carneiro,
     19-Feb-2014.) $)
  163prm $p |- ( ( 4 x. ( 4 x. 10 ) ) + 3 ) e. Prime $=
    ( c4 c10 cmul co c3 caddc c1 c2 3nn0 1nn0 2nn0 0nn0 eqtr4i c9 opreq1i c6 c7
    cc0 c5 c8 10nn nnnn0i deccl 3nn decnnclc decclc 3lt4 dec0u 4pos 2lt4 declti
    clt decltc dec10 3brtr3i eqbrtrri decnncl 1lt4 dec2dvds3 eqid dec0h addid2i
    1nn ax-1cn df-10 3t3e9 opreq12i 4nn nncni addid1i eqtri mulid1i 3cn 3eqtr4i
    df-4 decma2c eqtr3i 4p3e7 df-7 1lt3 ndvdsi 5nn 8nn nnmulcli dec8 2nn 5t2e10
    3t2e6 eqtr2i decmul2c 3eqtr2ri 3lt5 7nn dec7 dec5 4nn0 mulid2i addcomi df-5
    mul01i 5p5e10 decmac 2cn 3p2e5 3t3p2 2lt7 9nn 5p2e7 3eqtr4ri mulcomi 6p4e10
    dec9 3t3p3 2t2e4 declt 3eqtri nn0cni eqcomi 2p2e4 4t2e8 1p1e2 8p2e10 decadd
    dec4 df-3 6nn 6p2e8 prmlem2 ) AABCDZCDZEFDZYIEBBUAUBZUCZUDUEYIAAAECDZGFDZCD
    ZCDHFDZEGYMYPHYOEGIJUFZUCZKUFIJUGYIRFDZYIYQULBYLUHZBYPRHYLYSLKUIAHCDZHFDZYP
    RFDBYPULHYOHRKYRKLUJEGHUDJKUJUKUMUNYOYRUHUOUMUPUMYIEGBUAUQIJURUKYIYMUSEYKYP
    HFDZGUDYOHYRKUFVCYOHRGEYIEGUUDGYRKLJUUDUTGJVAZIIJYTYIEYOCDRGFDZFDUUAEGRGEBR
    GYOUUFIJLJYOUTZARCDZGFDGUUFUUEGVDVBZMILJBNGFDZEECDZUUFFDVEUUKNUUFGFVFUUIVGM
    ARFDZEGFDZAGCDZRFDZEGCDZGFDUULAUUMAAVHVIZVJVOVKUUNARFAUUQVLZOUUPEGFEVMVLZOV
    NZVPVQAEFDZPGFDZUUNEFDZEHCDZGFDUVAQUVBVRVSVKUUNAEFUUROUVDPGFWHOVNZVPVTWASYK
    ATCDZEWBTTWCUBZUCUDYJSUVFCDZEFYJRFDYJUVHYIYMUHTRYIRSRUVFSWBUBZUVGLTUVGUHLLS
    TCDZRFDUVJYTYIUVJUVJSTWBWCWDVIVJHRBRSRTUVIKLWELLSHCDZRFDUVKBUVKUVKSHWBWFWDV
    IVJWGWIUUHRFDZRSRCDRLVAZSSWBVIZWTMZWJUUAWKUVOWJVQOWLWAQYKASCDEFDZHWMSEUVIIU
    FWFSERHQYIESUVPHUVIILKUVPUTHKVAZQWMUBZIUVIYTYIQSCDRSFDZFDUUAGEGGSBRAQUVSJIJ
    JWNUUNGFDSUVSWOSUVNVBMUVILWPGSCDZGAFDZFDSSFDBUVTSUWASFSUVNWQUWAAGFDZSGAVDUU
    QWRWSMVGXAWIGGRGEARGSGJJLJWOUUEILJAUUMUUPUUFFDVOUUPEUUFGFUUSUUIVGMUUTVPXBVQ
    GERHESEHQHJILKWNUVQIIKGECDZRHFDZFDEHFDSUWCEUWDHFEVMWQHXCVBVGXDWIXEXBVPZXFWA
    UUBEFDZYKYNHFDZNHEKUDUEZEHIKUFXGEHHGUWFYIESUWGNIKKJUWGUTXLHEKIUFIUVIYTYIUWF
    ECDHSFDZFDUUAHEGEEBREUWFUWIKIJIUWFUTZSHFDQUWIUVCXHHSXCUVNWRWNXIILIHECDZGEFD
    ZFDPAFDBUWKPUWLAFUWKUVDPHEXCVMXJWHVKUWLUUMAGEVDVMWRVOMVGXKWIYNRFDYNUUKEFDEI
    UHZXMVKXBVQHERGHSEGUWFGKILJUWJUUEKIJSUWBHHCDZUUFFDWSUWNAUUFGFXNUUIVGMUVEXBV
    PUUBGFDZNUWFULXLHGEKJUDVTXOUPWAYOYKYNQEGIVCUEEIUCZWMEGGEYNYIEEYOQIJJIUUGWNU
    WPIIYTYIEYNCDUWLFDUUAERGREBRRYNUWLILJLUWMUUOAUUMUWLYDVOEGVMVDWRXPILLBUUJUUK
    GRFDZFDVEUUKNUWQGFVFGVDVJVGMUUHERCDZRFUUHRUWRAUUQWTEVMWTZMOVPVQYNGYNCDZEFUW
    TYNYNYNUWPXQWQXROXBUVCQYOULWNGEEGJIIJUGVTUMUPWAAACDZGFDZYKNBAGWPVCUENXGUBZU
    AAGHHNYIEHUXBBWPJKKUXBUTUNUXCIKYTYIANCDHHFDZFDUUAHGGRABRGNUXDKJJLXLUUOAUXDY
    DXSMWPLJUUBGGFDZFDTHFDZBUUBTUXEHFXTYAVGYBWIUUOUTVPVQHGRHHEGNCDZHKJLKUWONUXG
    XLNNXGVIWQMUVQHRFDZHHXCVJZXREHGFDGHFDYEHGXCVDWRVKYCXBUUCBUXBULUNHAHGKWPKJUJ
    UJUMUPWAUXAEFDZYKTUWFAEWPUDUEUVGUWHAEHETYIEPUXJUWFWPIKIUXJUTUWJUVGIPYFUBZYT
    YIUVFHPFDZFDUUAHRHRABRRTUXLKLKLWEPHFDTUXLUUBRFDYGHPXCPYFVIZWRWEXIWPLLUUBUXH
    FDUXFBUUBTUXHHFXTUXIVGYBWIUVLUTVPVQAPCDZETCDZEFUXNRFDUXNUXOPUXKUHHRPRERTIKL
    WELLUVDRFDPRFDPUVDPRFWHOPUXMVJWIUVLRUWRUVMUWSMWJVQOXBHAEEKWPIIUGUJUMWAUVPYK
    QHSEUVIUDUEZUVRWFYKQUVPCDZHFDUVPQCDZHFDUWEUXQUXRHFQUVPQWMVIUVPUXPVIXJOVKSEH
    WBIKUJUKWAYH $.
    $( [19-Feb-2014] $)

  $( 317 = 10331 base 4 is a prime number.  (Contributed by Mario Carneiro,
     19-Feb-2014.) $)
  317prm $p |- ( ( 4 x. ( ( 4 x. ( ( 4 x. 4 ) + 3 ) ) + 3 ) ) + 1 ) e. Prime $=
    ( c4 cmul co c3 caddc c1 4nn0 3nn0 c2 1nn0 2nn0 cc0 0nn0 c6 opreq1i decma2c
    eqtr4i c7 c5 c9 decclc 1nn decnnclc deccl 1lt4 3lt4 dec4 4pos 1lt3 eqbrtrri
    clt decltc dec0u breqtri 3nn declti dec2dvds1 6nn nnnn0i eqid dec0h addid2i
    2nn ax-1cn dec6 4nn nncni mul01i df-4 3cn mulid1i opreq12i 4p3e7 df-7 eqtri
    3t2e6 3eqtr4i 3p2e5 df-5 eqtr2i 2lt3 ndvdsi 5nn dec5 addcomi 3eqtri mulid2i
    addid1i 3eqtr4ri decmul1c nnmulcli 2lt5 7nn dec7 2cn 1p1e2 2p2e4 3t3p2 df-2
    decmac 2lt7 9nn dec9 mulcomi 0cn declt 3t3e9 df-3 opreq2i df-9 4t2e8 mulcli
    c8 2lt4 eqcomi mul12i c10 10nn dec10 decadd 5p2e7 df-10 4lt5 prmlem2 ) AAAA
    BCZDECZBCDECZBCFECZYGFYFDADGHUAZHUAZUBUCYGAAADBCZFECZBCZBCIECFFYJYMIYLDFHJU
    AZUDZKUAJJUEYFYMDIYIYOHKUFYFYMLECYMUKAYLDLGYNHMUFAFBCZLECZAYLUKUGFDLFJHMJUH
    UIULUJULYLYNUMUNULULYGFFYFDYIUOUCJJUEUPYGYJUQDYHAANBCZIECZBCFECZIUOYSFNINUR
    USZKUAZJUAVCYSFLIDYGFFYTIUUBJMKYTUTIKVAZHJJNILFDYFDFYSLFECZUUAKMJYSUTALBCZF
    ECZFUUDFJVAZFVDVBZQZHHJFILFDADFNUUDJKMJVEUUELFEAAVFVGZVHZOHHJADFECZDFBCZUUD
    ECVIUUMDUUDFEDVJVKZUUHVLQZADECZNFECZYPDECZDIBCZFECZUUPRUUQVMVNVOYPADEAUUJVK
    ZOUUSNFEVPOZVQZPUVCPAFECZDIECZYPFECZUUMIECZUVESUVDVRVSVTZYPAFEUVAOZUUMDIEUU
    NOZVQPWAWBSYHAYKDECZBCDECZIWCUVKDDDHHUAZHUAVCUVKDLISYGFAUVLIUVMHMKUVLUTUUCS
    WCUSZJGDDFLSYFDDUVKLAECZHHJMUVKUTYQAUVOUGAUUJVBQUVNHHFFFLDADLSFDECZJJJMWDYQ
    AUULUVPUGVIDFVJVDWEWFHHMAUULFDBCZFLECZECVIUVQDUVRFEDVJWGZFVDWHZVLQZDLECZDUV
    QLECZUUEDECZDVJWHZUVQDLEUVSOZDHVAZWIZWTUVKSDBCZUWILECFFDDDLSHJJWDHMUWCUWBDU
    WFUWEVTUWDDUVQUWGUVSQWJUWIUWISDWCUOWKVGWHQPFFLIDAFFSIJJMKWDUUCHJJAUULUVQUUD
    ECVIUVQDUUDFEUVSUUHVLQUVDUVEUVFUVQIECUVHUVIUVQDIEUVSOVQWTPWLWBRYHAAIBCZDECZ
    BCFECZIWMUWKFIDKHUAZJUAVCUWKFLIRYGFIUWLIUWMJMKUWLUTUUCRWMUSZJKIDLIRYFDSUWKL
    IECZKHMKUWKUTZUUELIEUUKOUWNHUVNFDFFIADFRLSECZJHJJWNUVFSUWQWDSSWCVGZVBQKHJFI
    BCZFFECZECIIECAUWSIUWTIEIWOWGZWPVLWQVTRUUQUURUUTVNWNUVBVQZWTFDLIDSDIRIJHMKW
    NUUCHHKUVQUWOECUVESUVQDUWOIEUVSIWOVBZVLVRVTWRWTPZFDLIFIFFRIJHMKWNUUCJJJIUWT
    FFBCZUUDECWSUXEFUUDFEFVDVKUUHVLQUVESUVGUVFVRUVJWDWIZWTPXAWBUWKYHARBCZTIDKUO
    UCZRUWNUDXBRLIFUWKYGFLUXGTUWNMKJRUWNUMXCUWMJMYGRUWKBCZUWOECUWKRBCZILECZECUX
    DUXIUXJUWOUXKERUWKRWMVGUWKUXHVGZXDLIXEWOWEVLVOUUEUWKLBCZFEUUELUXMUUKUWKUXLV
    HQOPUWJFECTUWKUKXCIFDKJUOUIXFUJWBYLYHYRSDFHUBUCZNUUAUDWCNLFFYLYGFLYRSUUAMJJ
    NUUAUMWDYNJMDFLFNYFDFYLUVRHJMJYLUTZUUFFUVRUUGUVTQUUAHJFILFDADFNUUDJKMJVEUUI
    HHJUUOUXBPRUUQUURFNBCZFECVNWNUXPNFENNURVGWGOVQWTUUEYLLBCZFEUUELUXQUUKYLYLUX
    NVGVHQOPUVFSYLUKWDFDFFJHJJUEUIULUJWBYEFECZYHYEIECZUWKAFGUBUCAIGKUAUXHAIIDUX
    RYGFTUXSUWKGKKHUXSUTZUWPAFGJUAJTXBUSAFIDAYFDFUXRITECZGJKHUXRUTZUWKDDBCZIECT
    IECUYAWRUYCTIEXGOTITXBVGWOWEWFGHJDIFECZYEEXHXIYPFABCDEAFUUJVDXDOWTAFLDITFFU
    XRDGJMHUYBUWGKJJTXMFECUWJUUDECXJUWJXMUUDFEXKUUHVLQIDECZSUWSDECUVFUYEUVESIDW
    OVJWEVRVOUWSIDEUXAOWDWIWTPIADFKGHJUFXNULWBYFYHYEYLADGUOUCAGUDZUXNADDFYEYGFY
    KYFYLGHHJYFUTUXOUYFJDHUDALDDAYFDLYEDYKECGMHHAGUMYKDADUUJVJXLVJWEGHMDUWBYEEU
    WBDUWEXOXIUWDUTPAYKBCZDYEBCZFEUYGADABCZBCUYHYKUYIABADUUJVJXDXIADAUUJVJUUJXP
    VOOWTDAFDHGJHUEUFULWBASBCDECZYHYLUXSSDUVNUOUCYNAIGVCUCSDAIYLYGFXQUYJUXSUVNH
    GKUYJUTUXTYNJXQXRUSDFDISYFDFYLAXQECHJHKUXOFLIIDIAXQJMKKUGXSDUYDFIECXHFIVDWO
    WEQUWOIUXCXOXTUVNHJFFFLDADLSUULJJJMWDYQAUULUGVIVOHHMUWAUWHWTSIECRSFBCZIECUU
    RYAUYKSIESUWRVKOWNWIPDFLIDXQFFYLIHJMKUXOUUCHJJXQTFECUYCUUDECYBUYCTUUDFEXGUU
    HVLQUXFPWTASIDGUVNKHXNYCULWBYD $.
    $( [19-Feb-2014] $)

  $( 631 = 21313 base 4 is a prime number. $)
  631prm $p |- ( ( 4 x. ( ( 4 x. ( ( 4 x. 9 ) + 3 ) ) + 1 ) ) + 3 ) e. Prime $=
    ( c4 c9 cmul co c3 caddc 3nn0 decclc 1nn0 2nn0 cc0 0nn0 eqid opreq1i eqtr4i
    c1 c2 decma2c c6 c5 9nn nnnn0i 3nn decnnclc deccl 3lt4 1lt4 clt dec9 decltc
    2lt3 eqbrtrri dec0u breqtri 1nn declti dec2dvds3 dec0h 4nn nncni mul01i 3cn
    nn0cni mulcli addid1i 00id opreq2i eqtr2i mulid1i decmul2c 3eqtr4ri c7 df-7
    3t3e9 dec7 3t2e6 3eqtr4i 1lt3 ndvdsi 5nn 7nn 4nn0 dec4 addid2i addcomi df-4
    ax-1cn opreq12i 5p4e9 dec5 mulid2i decmul1c 3p2e5 decmac df-3 mulcomi eqtri
    1lt5 2p2e4 6p3e9 1lt7 3eqtri dec6 3t3p2 2t2e4 4p2e6 4pos 1lt2 mul12i eqtr3i
    2cn 6nn eqcomi 2nn addassi 3eqtrri 2lt4 8nn dec8 df-5 df-9 4t2e8 10nn dec10
    c8 c10 decadd 5t2e10 decsuc 2lt5 prmlem2 ) AAABCDZEFDZCDPFDZCDEFDZYNEYMPBEB
    UAUBZGHZIHZUCUDYNAAAECDZPFDZCDZCDQFDZEPYRUUAQYTEPGIHZUEZJHZGIUFYMUUAPQYQUUD
    IJUGYMUUAKFDUUAUHBYTEKYPUUCGLUFAQCDZPFDZBYTUHUIQEPPJGIIUGUKUJULUJYTUUCUMZUN
    UJUJYNEPYMPYQUOUDGIUGUPYNYRUQEYOUUBPUCUUEUOUUAQKPEYNEPUUBPUUDJLIUUBMPIURZGG
    IYTKKPEYMPKUUAKPFDZUUCLLIUUHAKCDZKPFAAUSUTZVAZNGILEYTCDZKFDUUNUUNKKFDZFDYMU
    UNEYTVBYTUUCVCVDVEUUOKUUNFVFVGEPBEEKYTGGIYTMZGLEECDZKFDBKFDBUUQBKFVNNBBUAUT
    ZVEVHUUKEFDZEEPCDZEGURZEVBVIZOVJVKUUKEKCDZPFUUKKUVCUUMEVBVAONZRVLSPFDAPCDZE
    FDZEQCDZPFDVMVOUVGSPFVPNVQZRVRVSTYOAAVLCDEFDZCDQFDZPVTUVIQVLEVLWAUBZGHZJHUO
    UVIQKPTYNEQUVJPUVLJLIUVJMUUITVTUBZGJVLEKQTYMPAUVIKQFDZUVKGLJUVIMUUKKQFUUMNU
    VMIWBPEPKTBEEVLKAFDZIGILVOUVEKFDZAUVOWCAUULWDZOZUVMGGTPCDZPEFDZFDTAFDZBUVST
    UVTAFTTVTUTZVIZUVTEPFDZAPEWGVBWEWFOZWHWIVHYSEFDZTECDZUWGKFDZPPEEEKTGIIWJGLP
    ECDZKFDEKFDZEUWIEKFEVBWKZNEVBVEZVHUUSEUWIUVAUWKOWLUWGTEUWBVBVDVEOZRPPKQEAPP
    TQIILJWJQJURZGIIAUWDUWIUUJFDWFUWIEUUJPFUWKPWGWDZWHOZEQFDZTUWIQFDUVEPFDZWMUW
    IEQFUWKNWJVKZWNRPPKPQQEKTPIILIWJUUIJGLPQCDZUUOFDQKFDQUWTQUUOKFQXKWKZVFWHQXK
    VEVHEQPFDZUUSUWTPFDWOUVAUWTQPFUXANVQWNRWRVSVLYOAATCDZQFDZCDQFDZPWAUXDQTQUVM
    JHZJHUOUXDQKPVLYNEEUXEPUXFJLIUXEMUUIUVKGGTQKEVLYMPAUXDKEFDUVMJLGUXDMUUKKEFU
    UMNUVKIWBPEPKTBEEVLUVOIGILVOUVRUVMGGPTCDZUVTFDUWABUXGTUVTAFTUWBWKUWEWHWIVHU
    WFUWHETCDZKFDUWMUWGUXHKFTEUWBVBWPNWQWNPEKEQAPQVLEIGLGVOUVAJIJUWTUVNFDQQFDZA
    UWTQUVNQFUXAQXKWDZWHWSVHSEFDZBUVGEFDUUGWTUVGSEFVPNUIVKZWNRPEKPQEEPVLPIGLIVO
    UUIJGIEUXBUWTUUJFDWOUWTQUUJPFUXAUWOWHOUVHWNRXAVSUUFEFDZYOAYSQFDZCDPFDZAQEJU
    CUDUXNPEQGJHZIHUSUXNPPKUXMYNEQUXOAUXPIILUXOMWCQEJGHZGJEQKEUXMYMPSUXNPQFDZGJ
    LGUXNMUUSEUXBUXRUVAWOQPXKWGWEXBUXQISXLUBZQEPQEBEQUXMKSFDZJGIJUXMMZUVEQFDSUX
    TXCSSXLUTWDOGGJQECDZUXRFDUXKBUYBSUXREFUYBUVGSQEXKVBWPVPWQUXRUXBEPQWGXKWEWOO
    ZWHWTVHXDWNQEKEQSPQUXMEJGLGUYAUVAJIJQQCDZUVNFDAQFDSUYDAUVNQFXEUXJWHXFVHUXLW
    NRUXMPCDZKFDUYEUXMUYEUXMPUXMUXQVCZWGVDVEUXMUYFVIVHRUVPAUXMUHWCPQKEIJLGXGXHU
    JULVSYTYOAYSCDZVLEPGUOUDYSEGUEZUEZWAEPPEUYGYNEYSYTVLGIIGUUPVOUYIGUYHYSKEPEY
    MPKUYGPYSFDUYHLGIYSUYHUMYSPAEUULVBVDZWGWEGILYLEYSCDZEUWJFAUUQCDYLUYKUUQBACV
    NVGAEEUULVBVBXIXJUWJEUWLXMWHUVDRUYGPUYGCDZEFUYLUYGUYGAYSUULUYJVDWKXMNWNUVFV
    LYTUHVOPEEPIGGIUFVRUJULVSAACDZPFDZYOYLPFDZQAPWBUOUDBPYPIHZXNAPKQUYOYNEBUYNQ
    WBILJUYNMZUWNUYPGYPBPQPAYMPPUYOKBFDZYPIJIUYOMUUGBUYRUIBUURWDOWBIIEUXBYLFWOV
    GUWRMRPUYOCDZQFDUYOQFDYLUXRFDYMUYSUYOQFUYOUYOUYPVCWKNYLPQABUULUURVDWGXKXOUX
    REYLFUYCVGXPWNAPQUSIJXQUPVSUYMEFDZYOAYECDPFDZAAEWBUCUDYEPYEXRUBZIHUSYEPPKUY
    TYNEAVUAAVUBIILVUAMWCAEWBGHZGWBQKPPUYTYMPKYEPAFDZJLIIXSUWRTAPFDVUDWJXTAPUUL
    WGWEXBVUCILAEKPQBEPUYTPKFDZWBGLIUYTMUUKPFDPVUEUUIPWGVEOJGIBYEPFDUUFUUJFDYAU
    UFYEUUJPFYBUWOWHOUVHWNUUKUYTKCDZPFUUKKVUFUUMUYTUYTVUCVCZVAONRUYTPCDZKFDUYTK
    FDUYTVUHUYTKFUYTVUGVINUYTVUGVEVHRUVPAUYTUHWCPAKEIWBLGXGUGUJULVSUXCEFDZYOASC
    DEFDZYFTEUVMUCUDSEUXSGHYCSEQQVUIYNEUYNVUJYFUXSGJJVUJMYDTEUVMGHGAPWBIHTEAESY
    MPTVUIQUYNFDUVMGWBGVUIMZKQAPAEQUYNLJWBIUWNUYQUVOAUVQXMWOYGUXSIUVMPQQPTBEQSA
    TFDZIJJIXCUWABVULUUGWIATUULUWBWEUIVKUVMGJUVSUXIFDUWABUVSTUXIAFUWCWSWHWIVHQQ
    ETQCDZJJWOUUFQFDZYFVUMYDYHOYIRPQKEETPQSEIJLGXCUVAGIJUUTUVNFDUWQTUUTEUVNQFUV
    BUXJWHWMVHUXLRWNTEKQEUYNEQVUIQUVMGLJVUKUWNGGJPPKQEAPPTUVNIILJWJUUKQFDQUVNUW
    NUXJOGIIUWPUWSWNXDWNRVUNYFVUIUHYDQTQEJUVMJGXQYJUJULVSYK $.
    $( [1-Mar-2014] $)

