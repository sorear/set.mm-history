$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        New definitions using the mapping operator
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$( ==== still needs work =================================================== $)

  $(
  @c ~~>t @.  @( Limit arrow. @)

  @( Extend class notation with a function on topological spaces whose value is
     the convergence relation for limit sequences in the space. @)
  clm @a class ~~>t @.

  @{
    @d d f x e j k m @.
    @( Define a function on topologies whose value is the convergence
       relation for the space.  Although ` f ` is typically a function from
       upper integers to the topological space, it doesn't have to be.
       Unfortunately, the value of the function must exist to use
       ~ fvmpt , and we use the otherwise unnecessary conjunct
       ` dom f C_ CC ` to ensure that. @)
    df-lmNEW @a |- ~~>t = ( j e. Top |-> { <. f , x >. |
      ( f e. ( U. j ^pm CC ) /\ x e. U. j /\ A. u e. j ( x e. u ->
        E. n e. ZZ ( f |` ( ZZ>= ` n ) ) : ( ZZ>= ` n ) --> u ) ) } ) @.

    @( Define a function on metric spaces whose value is the set of Cauchy
       sequences of the space. @)
    df-cauNEW @a |- Cau = ( d e. Met |-> { f e. ( dom dom d ^pm CC ) |
       A. x e. RR+ E. k e. ZZ ( f |` ( ZZ>= ` k ) ) :
          ( ZZ>= ` k ) --> ( ( f ` k ) ( ball ` d ) x ) } ) @.

    @( Define the class of complete metrics. @)
    df-cmetNEW @a |- CMet = { d e. Met | A. f e. ( Cau ` d )
           E. x e. dom dom d f ( ~~>t ` ( MetOpen ` d ) ) x } @.
  @}
  $)

$( ==== df-plpq, etc. & df-plpr, etc. (incomplete) ========================= $)

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y D $.  $d x y z F $.
    $d x y z S $.  $d u v w z R $.  $d u v w x y z $.
    ecopoprNEW.1 $e |- R = { <. x , y >. |
      ( ( x e. ( S X. S ) /\ y e. ( S X. S ) ) /\
        ( ( 1st ` x ) F ( 2nd ` y ) ) = ( ( 1st ` y ) F ( 2nd ` x ) ) ) } $.
    $( This is the first of several theorems about equivalence relations of the
       kind used in construction of fractions and signed reals, involving
       operations on equivalent classes of ordered pairs.  This theorem
       expresses the relation ` R ` (specified by the hypothesis) in terms of
       its operation ` F ` . $)
    ecopopreq2 $p |- ( ( A e. ( S X. S ) /\ B e. ( S X. S ) ) ->
        ( A R B <-> ( ( 1st ` A ) F ( 2nd ` B ) ) =
                    ( ( 1st ` B ) F ( 2nd ` A ) ) ) ) $=
      ( cxp wcel wa wbr c1st cfv c2nd co wceq cv eleq1 anbi1d fveq2 opreq1d
      opreq2d eqeq12d anbi12d anbi2d brabg ibar bitr4d ) CFFIZJZDUJJZKZCDELUMCM
      NZDONZGPZDMNZCONZGPZQZKZUTARZUJJZBRZUJJZKZVBMNZVDONZGPZVDMNZVBONZGPZQZKUK
      VEKZUNVHGPZVJURGPZQZKVAABCDUJUJEVBCQZVFVNVMVQVRVCUKVEVBCUJSTVRVIVOVLVPVRV
      GUNVHGVBCMUAUBVRVKURVJGVBCOUAUCUDUEVDDQZVNUMVQUTVSVEULUKVDDUJSUFVSVOUPVPU
      SVSVHUOUNGVDDOUAUCVSVJUQURGVDDMUAUBUDUEHUGUMUTUHUI $.
      $( [15-Sep-2013] $)

    $( This theorem expresses the relation ` R ` (specified by the hypothesis)
       in terms of its operation ` F ` . $)
    ecopopreqNEW $p |- ( ( ( A e. S /\ B e. S ) /\
                   ( C e. S /\ D e. S ) ) ->
        ( <. A , B >. R <. C , D >. <-> ( A F D ) = ( C F B ) ) ) $=
      ( wcel wa cop wbr c1st cfv c2nd co wceq cxp wb opelxpi ecopopreq2 syl2an
      op1stg adantr op2ndg opreq12 ancoms eqeq12d bitrd ) CHKZDHKZLZEHKZFHKZLZL
      ZCDMZEFMZGNZUSOPZUTQPZIRZUTOPZUSQPZIRZSZCFIRZEDIRZSUNUSHHTZKUTVKKVAVHUAUQ
      CDHHUBEFHHUBABUSUTGHIJUCUDURVDVIVGVJUNVBCSZVCFSVDVISUQULVLUMCDHUEUFEFHHUG
      VBCVCFIUHUDUNVFDSZVEESZVGVJSZUQCDHHUGUOVNUPEFHUEUFVNVMVOVEEVFDIUHUIUDUJUK
      $.
      $( [15-Sep-2013] $) $( [16-Aug-1995] $)

    $( Assuming the operation ` F ` is commutative, compute the domain the
       relation ` R ` specified by the first hypothesis. $)
    ecopoprdmNEW $p |- dom R = ( S X. S ) $=
      ( vz cdm cxp wss cv wcel wa c1st cfv c2nd co wceq copab opabssxp eqsstri
      dmss ax-mp dmxpid sseqtri wbr eqid ecopopreq2 mpbiri anidms visset
      breldm syl ssriv eqssi ) CHZDDIZUPUQUQIZHZUQCURJUPUSJCAKZUQLBKZUQLMUTNOVA
      POEQVANOUTPOEQRZMABSURFVBABUQUQTUACURUBUCUQUDUEGUQUPGKZUQLZVCVCCUFZVCUPLV
      DVEVDVDMVEVCNOVCPOEQZVFRVFUGABVCVCCDEFUHUIUJVCVCCGUKULUMUNUO $.
      $( [16-Sep-2013] $) $( [27-Aug-1995] $)

    ${
      ecopoprNEW.2 $e |- B e. _V $.
      $( Assuming the operation ` F ` is commutative, show that the relation
         ` R ` , specified by the first hypothesis, is symmetric. $)
      ecopoprsymNEW $p |- ( A R B -> B R A ) $=
        ( wbr cxp wcel wa wb cv c1st cfv c2nd co wceq copab opabssxp eqsstri
        brel ecopopreq2 ancoms eqcom syl6bb bitr4d syl ibi ) CDEJZDCEJZULCFFKZL
        ZDUNLZMZULUMNCDUNUNEIEAOZUNLBOZUNLMURPQUSRQGSUSPQURRQGSTZMABUAUNUNKHUTA
        BUNUNUBUCUDUQULCPQDRQGSZDPQCRQGSZTZUMABCDEFGHUEUQUMVBVATZVCUPUOUMVDNABD
        CEFGHUEUFVBVAUGUHUIUJUK $.
        $( [16-Sep-2013] $) $( [27-Aug-1995] $)
    $}

    ${
      $( Assume the operation is closed. $)
      ecopoprNEW.cl $e |- ( ( x e. S /\ y e. S ) -> ( x F y ) e. S ) $.
      $( Assume the operation is commutative. $)
      ecopoprNEW.com $e |- ( x F y ) = ( y F x ) $.
      $( Assume the operation is associative. $)
      ecopoprNEW.ass $e |- ( ( x F y ) F z ) = ( x F ( y F z ) ) $.
      $( Assume the operation has the cancellation property. $)
      ecopoprNEW.can $e |- ( ( x e. S /\ y e. S ) ->
                 ( ( x F y ) = ( x F z ) -> y = z ) ) $.
      ${
        ecopoprNEW.3 $e |- B e. _V $.
        ecopoprNEW.4 $e |- C e. _V $.
        $( Assuming that operation ` F ` is closed (second hypothesis),
           commutative (third hypothesis), associative (fourth hypothesis), and
           has the cancellation property (fifth hypothesis), show that the
           relation ` R ` , specified by the first hypothesis, is
           transitive. $)
        ecopoprtrnNEW $p |- ( ( A R B /\ B R C ) -> A R C ) $=
          ( cxp wcel wa wbr cv c1st cfv c2nd co wceq copab opabssxp eqsstri
          brel simpld anim12i wi w3a wb ecopopreq2 3adant3 3adant1 anbi12d
          opreq12 syl6bi fvex caopr411 oprex caoprcom eqtri eqeq12i syl6ib
          xp1st xp2nd caoprcl syl11anc 3ad2ant2 syl2an 3adant2 caoprcan syld
          sylibrd 3expb mpcom ) DHHQZRZEWARZFWARZSZSDEGTZEFGTZSZDFGTZWFWBWGWEWF
          WBWCDEWAWAGOGAUAZWARBUAZWARSWJUBUCWKUDUCIUEWKUBUCWJUDUCIUEUFZSABUGWAW
          AQJWLABWAWAUHUIZUJUKEFWAWAGPWMUJULWBWCWDWHWIUMWBWCWDUNZWHDUBUCZFUDUCZ
          IUEZFUBUCZDUDUCZIUEZUFZWIWNWHEUBUCZEUDUCZIUEZWQIUEZXDWTIUEZUFZXAWNWHW
          OXCIUEZXBWPIUEZIUEZXBWSIUEZWRXCIUEZIUEZUFZXGWNWHXHXKUFZXIXLUFZSXNWNWF
          XOWGXPWBWCWFXOUOWDABDEGHIJUPUQWCWDWGXPUOWBABEFGHIJUPURUSXHXKXIXLIUTVA
          XJXEXMXFABCWOXCXBWPIDUBVBEUDVBZEUBVBZLMFUDVBVCXMWTXDIUEXFABCXBWSWRXCI
          XRDUDVBFUBVBLMXQVCABWTXDIWRWSIVDZXBXCIVDLVEVFVGVHWNXDHRZWQHRZXGXAUMWC
          WBXTWDWCXBHRXCHRXTEHHVIEHHVJABXBXCHIKVKVLVMWBWDYAWCWBWOHRWPHRYAWDDHHV
          IFHHVJABWOWPHIKVKVNVOABCXDWQWTHIXSNVPVLVQWBWDWIXAUOWCABDFGHIJUPVOVRVS
          VT $.
          $( [16-Sep-2013] $) $( [11-Feb-1996] $)
      $}

      $( Assuming that operation ` F ` is closed (second hypothesis),
         commutative (third hypothesis), associative (fourth hypothesis), and
         has the cancellation property (fifth hypothesis), show that the
         relation ` R ` , specified by the first hypothesis, is an equivalence
         relation. $)
      ecopoprerNEW $p |- Er R $=
        ( vu vv vw cv visset ecopoprsymNEW ecopoprtrnNEW ster ) LMNDABLOZMOZDEF
        GMPZQABCTUANODEFGHIJKUBNPRS $.
        $( [16-Sep-2013] $) $( [16-Feb-1996] $)
    $}
  $}

  ${
    $d a b c d x y z A $.  $d a b c d x y z B $.  $d a b d y z C $.
    $d x y M $.  $d a b d y z D $.  $d a b c d x y z G $.
    $d a b c d x y z R $.  $d x y Q $.  $d a b c d x y z S $.  $d x y F $.
    $d x K $.  $d y H $.
    oprecNEW.1 $e |- H e. _V $.
    oprecNEW.4 $e |- R e. _V $.
    oprecNEW.5 $e |- Er R $.
    $( Equivalence relation. $)
    oprecNEW.7 $e |- R = { <. x , y >. |
      ( ( x e. ( S X. S ) /\ y e. ( S X. S ) ) /\
        ( ( 1st ` x ) M ( 2nd ` y ) ) = ( ( 1st ` y ) M ( 2nd ` x ) ) ) } $.
    $( Pre-operation. $)
    oprecNEW.10 $e |- G = ( x e. ( S X. S ) , y e. ( S X. S ) |-> J ) $.
    $( Substitutions for pre-operation. $)
    oprecNEW.12 $e |- ( x = <. A , B >. -> J = K ) $.
    oprecNEW.13 $e |- ( y = <. C , D >. -> K = H ) $.
    $( Main operation. $)
    oprecNEW.14 $e |- F = ( x e. Q , y e. Q |->
      ( iota z E. a E. b ( x = [ a ] R /\ y = [ b ] R /\
               z = [ ( a G b ) ] R ) ) ) $.
    $( Quotient class for main operation. $)
    oprecNEW.15 $e |- Q = ( ( S X. S ) /. R ) $.
    $( Lemma to establish compatibility of pre-operation. $)
    oprecNEW.16 $e |- ( ( ( c e. ( S X. S ) /\ a e. ( S X. S ) ) /\
                       ( d e. ( S X. S ) /\ b e. ( S X. S ) ) ) ->
                ( ( c R a /\ d R b ) -> ( c G d ) R ( a G b ) ) ) $.
    $( Express an operation on equivalence classes of ordered pairs in terms of
       equivalence class of operations on ordered pairs.  See set.mm for
       additional comments describing the hypotheses.  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 4-Jun-2013.) $)
    oprecNEW $p |- ( ( ( A e. S /\ B e. S ) /\ ( C e. S /\ D e. S ) ) ->
                ( [ <. A , B >. ] R F [ <. C , D >. ] R ) =
                   [ H ] R ) $=
      ( wcel wa cop cec co cv wceq w3a wex cio ecopqsi eqeq1 3anbi1d 2exbidv
      iotabidv 3anbi2d iotaex ovmpt2 syl2an cvv ecexg ax-mp wb df-3an wbr cxp
      opelxpi anim12i adantr opex visset ecopoprdmNEW ereldm syl5ibcom
      im2anan9 imp cdm syl6eleqr erthdm syl bi2anan9 biimpa wi breq1 anbi1d
      opreq1 breq1d imbi12d imbi2d anbi2d opreq2 an4s ex vtocl2ga 3imp
      syl111anc oprex erthi eqeq2d biimpar expl syl5bi 19.23advv eqid eceq2
      adantl opreq12 3anbi123d cla42ev mp3an 3anbi3d mpbiri a1i impbid iota5
      mpan2 eqtrd ) DJUKEJUKULZFJUKGJUKULZULZDEUMZIUNZFGUMZIUNZKUOZYLQUPZIUNZUQ
      ZYNRUPZIUNZUQZCUPZYPYSLUOZIUNZUQZURZRUSQUSZCUTZMIUNZYHYLHUKYNHUKYOUUHUQYI
      JDEIHUBUIVAJFGIHUBUIVAABYLYNHHAUPZYQUQZBUPZYTUQZUUEURZRUSQUSZCUTUUHKYRUUM
      UUEURZRUSQUSZCUTUUJYLUQZUUOUUQCUURUUNUUPQRUURUUKYRUUMUUEUUJYLYQVBVCVDVEUU
      LYNUQZUUQUUGCUUSUUPUUFQRUUSUUMUUAYRUUEUULYNYTVBVFVDVEUHUUGCVGVHVIYJUUHYKY
      MLUOZIUNZUUIYJUVAVJUKZUUHUVAUQIVJUKUVBUBUUTVJIVKVLYJUUGCUVAVJYJUUGUUBUVAU
      QZVMUVBYJUUGUVCYJUUFUVCQRUUFYRUUAULZUUEULYJUVCYRUUAUUEVNYJUVDUUEUVCYJUVDU
      LZUVCUUEUVEUVAUUDUUBUVEUUTUUCIVOZUVAUUDUQZUVEYKJJVPZUKZYMUVHUKZULZYPUVHUK
      ZYSUVHUKZULZYKYPIVOZYMYSIVOZULZUVFYJUVKUVDYHUVIYIUVJDEJJVQZFGJJVQZVRVSYJU
      VDUVNYHYRUVLYIUUAUVMYHUVIYRUVLUVRYKYPUVHIDEVTZQWAZUCABIJPUDWBZWCWDYIUVJUU
      AUVMUVSYMYSUVHIFGVTZRWAZUCUWBWCWDWEWFYJUVDUVQYHYRUVOYIUUAUVPYHYKIWGZUKYRU
      VOVMYHYKUVHUWEUVRUWBWHYKYPIUWAUCWIWJYIYMUWEUKUUAUVPVMYIYMUVHUWEUVSUWBWHYM
      YSIUWDUCWIWJWKWLUVKUVNUVQUVFUVNSUPZYPIVOZTUPZYSIVOZULZUWFUWHLUOZUUCIVOZWM
      ZWMUVNUVOUWIULZYKUWHLUOZUUCIVOZWMZWMUVNUVQUVFWMZWMSTYKYMUVHUVHUWFYKUQZUWM
      UWQUVNUWSUWJUWNUWLUWPUWSUWGUVOUWIUWFYKYPIWNWOUWSUWKUWOUUCIUWFYKUWHLWPWQWR
      WSUWHYMUQZUWQUWRUVNUWTUWNUVQUWPUVFUWTUWIUVPUVOUWHYMYSIWNWTUWTUWOUUTUUCIUW
      HYMYKLXAWQWRWSUWFUVHUKZUWHUVHUKZULUVNUWMUXAUVLUXBUVMUWMUJXBXCXDXEXFUUTUUC
      IYKYMLXGYPYSLXGUCXHWJXIXJXKXLXMUVCUUGWMYJUVCUUGYRUUAUVGURZRUSQUSZYLYLUQZY
      NYNUQZUVAUVAUQZUXDYLXNYNXNUVAXNUXCUXEUXFUXGURQRYKYMUVTUWCYPYKUQZYSYMUQZUL
      ZYRUXEUUAUXFUVGUXGUXJYQYLYLUXHYQYLUQUXIYPYKIXOVSXIUXJYTYNYNUXIYTYNUQUXHYS
      YMIXOXPXIUXJUUDUVAUVAUXJUUCUUTUQUUDUVAUQYPYKYSYMLXQUUCUUTIXOWJXIXRXSXTUVC
      UUFUXCQRUVCUUEUVGYRUUAUUBUVAUUDVBYAVDYBYCYDVSYEYFYJUUTMUQZUVAUUIUQYHUVIUV
      JUXKYIUVRUVSABYKYMUVHUVHNMLOUFUGUEUAVHVIUUTMIXOWJYGYG $.
      $( [17-Sep-2013] $) $( [6-Aug-1995] $)
  $}

  ${
    $d x y $.
    $( Define pre-addition on positive fractions.  This is a "temporary" set
       used in the construction of complex numbers ~ df-c , and is intended to
       be used only by the construction.  This "pre-addition" operation works
       works directly with ordered pairs of integers.  The actual positive
       fraction addition ` +Q ` ( ~ df-plq ) works with the equivalence classes
       of these ordered pairs determined by the equivalence relation ` ~Q `
       ( ~ df-enq ).  (Analogous remarks apply to the other "pre-" operations
       in the complex number construction that follows.)  From Proposition
       9-2.3 of [Gleason] p. 117. $)
    df-plpqNEW $a |- +pQ = ( x e. ( N. X. N. ) , y e. ( N. X. N. ) |->
      <. ( ( ( 1st ` x ) .N ( 2nd ` y ) ) +N ( ( 1st ` y ) .N ( 2nd ` x ) ) ) ,
           ( ( 2nd ` x ) .N ( 2nd ` y ) ) >. ) $.

    $( Define pre-multiplication on positive fractions.  This is a "temporary"
       set used in the construction of complex numbers ~ df-c , and is intended
       to be used only by the construction.  From Proposition 9-2.4 of
       [Gleason] p. 119. $)
    df-mpqNEW $a |- .pQ = ( x e. ( N. X. N. ) , y e. ( N. X. N. ) |->
         <. ( ( 1st ` x ) .N ( 1st ` y ) ) ,
            ( ( 2nd ` x ) .N ( 2nd ` y ) ) >. ) $.

    $( Define equivalence relation for positive fractions.  This is a
       "temporary" set used in the construction of complex numbers ~ df-c , and
       is intended to be used only by the construction.  From Proposition 9-2.1
       of [Gleason] p. 117. $)
    df-enqNEW $a |- ~Q = ( ( ( N. X. N. ) X. ( N. X. N. ) )
                     \ ( <pQ u. `' <pQ ) ) $.
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.
    $( Equivalence relation for positive fractions in terms of positive
       integers.  (Contributed by Mario Carneiro, 8-May-2013.) $)
    ordpipq2 $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) ) ->
          ( A <pQ B <-> ( ( 1st ` A ) .N ( 2nd ` B ) ) <N
                        ( ( 1st ` B ) .N ( 2nd ` A ) ) ) ) $=
      ( vx vy cnpi cxp wcel wa cltpq wbr c1st cfv c2nd cmi co clti cv wceq
      eleq1 anbi1d fveq2 opreq1d opreq2d breq12d anbi12d anbi2d df-ltpq brabg
      ibar bitr4d ) AEEFZGZBUKGZHZABIJUNAKLZBMLZNOZBKLZAMLZNOZPJZHZVACQZUKGZDQZ
      UKGZHZVCKLZVEMLZNOZVEKLZVCMLZNOZPJZHULVFHZUOVINOZVKUSNOZPJZHVBCDABUKUKIVC
      ARZVGVOVNVRVSVDULVFVCAUKSTVSVJVPVMVQPVSVHUOVINVCAKUAUBVSVLUSVKNVCAMUAUCUD
      UEVEBRZVOUNVRVAVTVFUMULVEBUKSUFVTVPUQVQUTPVTVIUPUONVEBMUAUCVTVKURUSNVEBKU
      AUBUDUECDUGUHUNVAUIUJ $.
      $( [15-Sep-2013] $) $( [27-Aug-1995] $)

    ordpipqNEW.1 $e |- A e. _V $.
    ordpipqNEW.2 $e |- B e. _V $.
    ordpipqNEW.3 $e |- C e. _V $.
    ordpipqNEW.4 $e |- D e. _V $.
    $( Ordering of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    ordpipqNEW $p |- ( <. A , B >. <pQ <. C , D >. <->
                          ( A .N D ) <N ( C .N B ) ) $=
      ( vx vy cop cltpq wbr cnpi cxp wcel wa cmi co clti opex cv c1st cfv c2nd
      copab df-ltpq opabssxp eqsstri brel oprex ltrelpi dmmulpi 0npi ndmoprrcl
      simpld simprd opelxpi syl2an ancoms jca syl ordpipq2 op1st op2nd
      opreq12i breq12i syl6bb pm5.21nii ) ABKZCDKZLMZVJNNOZPZVKVMPZQZADRSZCBRSZ
      TMZVJVKVMVMLCDUALIUBZVMPJUBZVMPQVTUCUDWAUEUDRSWAUCUDVTUEUDRSTMZQIJUFVMVMO
      IJUGWBIJVMVMUHUIUJVSVQNPZVRNPZQZVPVQVRNNTCBRUKULUJWEVNVOWCANPZBNPZVNWDWCW
      FDNPZADNRHUMUNUOZUPWDCNPZWGCBNRFUMUNUOZUQABNNURUSWCWHWJVOWDWCWFWHWIUQWDWJ
      WGWKUPWJWHVOCDNNURUTUSVAVBVPVLVJUCUDZVKUEUDZRSZVKUCUDZVJUEUDZRSZTMVSVJVKV
      CWNVQWQVRTWLAWMDRABEVDCDGHVEVFWOCWPBRCDGVDABEFVEVFVGVHVI $.
      $( [15-Sep-2013] $) $( [8-May-2013] $)
  $}

  ${
    $d x y z $.
    $( Equivalence relation for positive fractions in terms of positive
       integers.  This was the original definition, before the shorter ~ df-enq
       was discovered.  (Contributed by Mario Carneiro, 8-May-2013.) $)
    dfenq2 $p |- ~Q = { <. x , y >. |
      ( ( x e. ( N. X. N. ) /\ y e. ( N. X. N. ) ) /\
        ( ( 1st ` x ) .N ( 2nd ` y ) ) = ( ( 1st ` y ) .N ( 2nd ` x ) ) ) } $=
      ( vz cnpi cxp cltpq ccnv cun cdif cv cop wceq wcel wa c1st cfv c2nd cmi
      co wex cab ceq copab wn eldif elxp anbi1i 19.41vv clti wbr wo eleq1
      df-br brun bitr3i syl6bb ordpipq2 wb visset brcnv syl5bb ancoms orbi12d
      sylan9bb notbid wor ltsopi xp1st xp2nd mulclpi syl2an syl2anc jca
      sotrieq sylancr adantl bitr4d pm5.32i anass bitri 2exbii 3bitri abbi2i
      df-enqNEW df-opab 3eqtr4i ) DDEZWGEZFFGZHZIZCJZAJZBJZKZLZWMWGMZWNWGMZNZWM
      OPZWNQPZRSZWNOPZWMQPZRSZLZNZNZBTATZCUAUBXGABUCXICWKWLWKMWLWHMZWLWJMZUDZNW
      PWSNZBTATZXLNZXIWLWHWJUEXJXNXLABWLWGWGUFUGXOXMXLNZBTATXIXMXLABUHXPXHABXPX
      MXFNXHXMXLXFXMXLXBXEUIUJZXEXBUIUJZUKZUDZXFXMXKXSWPXKWMWNFUJZWMWNWIUJZUKZW
      SXSWPXKWOWJMZYCWLWOWJULYDWMWNWJUJYCWMWNWJUMWMWNFWIUNUOUPWSYAXQYBXRWMWNUQW
      RWQYBXRURYBWNWMFUJWRWQNXRWMWNFAUSBUSUTWNWMUQVAVBVCVDVEWSXFXTURZWPWSDUIVFX
      BDMZXEDMZNYEVGWSYFYGWQWTDMXADMYFWRWMDDVHWNDDVIWTXAVJVKWRXCDMXDDMYGWQWNDDV
      HWMDDVIXCXDVJVLVMDXBXEUIVNVOVPVQVRWPWSXFVSVTWAUOWBWCWDXGABCWEWF $.
      $( [17-Sep-2013] $) $( [27-Aug-1995] $)
  $}

  ${
    $d x y z w v u $.
    $( The equivalence relation for positive fractions exists. $)
    enqexNEW $p |- ~Q e. _V $=
      ( ceq cnpi cxp cltpq ccnv cun cdif cvv df-enqNEW wcel niex xpex difexg
      ax-mp eqeltri ) ABBCZPCZDDEFZGZHIQHJSHJPPBBKKLZTLQRHMNO $.
      $( [17-Sep-2013] $) $( [3-Sep-1995] $)
  $}

  ${
    $d a x y A $.  $d a b c x y z $.
    $( There is a unique element of ` Q. ` equivalent to each element of
       ` N. X. N. ` .  (Contributed by Mario Carneiro, 28-Apr-2013.) $)
    nqereuNEW $p |- ( A e. ( N. X. N. ) -> E! x e. Q. x ~Q A ) $=
      ( vy va vz vb vc cnpi cxp wcel cv ceq wbr cnq wrex wa wceq wi wral wreu
      c2nd cfv con0 csuc xp2nd pion suceloni 3syl fvex sucid eleq2 rcla4ev
      sylancl imbi1d ralbidv clti wn rexnal pm4.63 wb ltpiord syl2anc anbi2d
      syl5bb rexbidva syl5bbr w3a rcla4cva fveq2 eleq1d breq2 rexbidv imbi12d
      rcla4cv syl 3imp visset enqer ersym ertr expcom reximdv syl5com 3expia
      com23 imp3a r19.23adva ex com3r syl6bi com13 cmi dfenq2 ecopoprdmNEW
      erref2 df-nq rabeq2i biimpri breq1 sylsyld com12 a1dd pm2.61d2 r19.21aiv
      cbvralv sylib a1i tfis2 rcla4v syl5 r19.23adv mpd anbi12d 2ralbidv
      sylan2 c1st cop co mulcompi elpqn breq2d notbid elrab2 simprbi breq1d
      rcla4va syl2an ancoms imp wo wor ltsopi sotric mpan notnot syl6bbr
      adantr mpbid ord mt3d opreq2d syl5eq ecopopreq2 biimpa eqtrd syl6eq
      ad2antrr xp1st mulcanpi syl11anc opeq12d wrel relxp 1st2nd sylancr
      ad2antlr 3eqtr4d rgen2a vtoclg reu4 sylanbrc ) BHHIZJZAKZBLMZANOZUVECKZBL
      MZPZUVDUVGQZRZCNSANSZUVEANTUVCBUAUBZUVGJZCUCOZUVFUVCUVMUDZUCJZUVMUVPJZUVO
      UVCUVMHJUVMUCJUVQBHHUEUVMUFUVMUGUHUVMBUAUIUJUVNUVRCUVPUCUVGUVPUVMUKULUMUV
      CUVNUVFCUCUVGUCJZDKZUAUBZUVGJZUVDUVTLMZANOZRZDUVBSZUVCUVNUVFRZUWFUWAEKZJZ
      UWDRZDUVBSZCEUVGUWHQZUWEUWJDUVBUWLUWBUWIUWDUVGUWHUWAUKUNUOUWKEUVGSZUWFRUV
      SUWMFKZUAUBZUVGJZUVDUWNLMZANOZRZFUVBSUWFUWMUWSFUVBUWMUWNGKZLMZUWTUAUBZUWO
      UPMZUQRZGUVBSZUWNUVBJZUWSRUXFUXEUQZUWMUWSUXFUXGUXAUXBUWOJZPZGUVBOZUWMUWSR
      UXGUXDUQZGUVBOUXFUXJUXDGUVBURUXFUXKUXIGUVBUXKUXAUXCPUXFUWTUVBJZPZUXIUXAUX
      CUSUXMUXCUXHUXAUXLUXBHJUWOHJUXCUXHUTUXFUWTHHUEUWNHHUEUXBUWOVAVBVCVDVEVFUW
      MUWPUXJUWRUWMUWPUXJUWRRUWMUWPPZUXIUWRGUVBUXNUXLPZUXAUXHUWRUXOUXHUXAUWRUXN
      UXLUXHUXAUWRRUXNUXLUXHVGUVDUWTLMZANOZUXAUWRUXNUXLUXHUXQUXNUWAUWOJZUWDRZDU
      VBSZUXLUXHUXQRZRUWKUXTEUWOUVGUWHUWOQZUWJUXSDUVBUYBUWIUXRUWDUWHUWOUWAUKUNU
      OVHUXSUYADUWTUVBUVTUWTQZUXRUXHUWDUXQUYCUWAUXBUWOUVTUWTUAVIVJUYCUWCUXPANUV
      TUWTUVDLVKVLVMVNVOVPUXAUXPUWQANUXAUWTUWNLMZUXPUWQRUWNUWTLFVQZGVQZVRVSUXPU
      YDUWQUVDUWTUWNLAVQZUYFUYEVRVTWAVOWBWCWDWEWFWGWHWIWJWKUXEUXFUWRUWPUXFUXEUW
      RUXFUWNUWNLMZUXEUWNNJZUWRUWNUVBLVRACLHWLACWMWNWOUXFUXEUYIUYIUXFUXEPUXEFNU
      VBFGWPWQWRWHUYIUYHUWRUWQUYHAUWNNUVDUWNUWNLWSULWAWTXAXBXCXDUWSUWEFDUVBUWNU
      VTQZUWPUWBUWRUWDUYJUWOUWAUVGUWNUVTUAVIVJUYJUWQUWCANUWNUVTUVDLVKVLVMXEXFXG
      XHUWEUWGDBUVBUVTBQZUWBUVNUWDUVFUYKUWAUVMUVGUVTBUAVIVJUYKUWCUVEANUVTBUVDLV
      KZVLVMXIXJXKXLUWCUVGUVTLMZPZUVJRZCNSANSUVLDBUVBUYKUYOUVKACNNUYKUYNUVIUVJU
      YKUWCUVEUYMUVHUYLUVTBUVGLVKXMUNXNUYOACNUYNUVDUVGLMZUVDNJZUVGNJZPZUVJUYMUW
      CUVTUVGLMUYPUVGUVTLCVQZDVQZVRVSUVDUVTUVGLUYGVUAUYTVRVTXOUYSUYPUVJUYSUYPPZ
      UVDXPUBZUVDUAUBZXQZUVGXPUBZUVGUAUBZXQZUVDUVGVUBVUCVUFVUDVUGVUBVUDVUCWLXRZ
      VUDVUFWLXRZQZVUCVUFQZVUBVUIVUFVUDWLXRZVUJVUBVUIVUCVUGWLXRZVUMVUBVUIVUCVUD
      WLXRVUNVUDVUCUVDUAUIZUVDXPUIXSVUBVUDVUGVUCWLVUBVUDVUGQZVUGVUDUPMZUYSUYPVU
      QUQZUYRUYQUYPVURRZUYRUVGUVBJZUVDUWHLMZUWHUAUBZVUDUPMZUQZRZEUVBSZVUSUYQUVG
      XTZUYQUVDUVBJZVVFUVGUWHLMZVVBVUGUPMZUQZRZEUVBSZVVFCUVDUVBNUVGUVDQZVVLVVEE
      UVBVVNVVIVVAVVKVVDUVGUVDUWHLWSVVNVVJVVCVVNVUGVUDVVBUPUVGUVDUAVIYAYBVMUOCE
      WPZYCYDVVEVUSEUVGUVBUWHUVGQZVVAUYPVVDVURUWHUVGUVDLVKVVPVVCVUQVVPVVBVUGVUD
      UPUWHUVGUAVIYEYBVMYFYGYHYIVUBVUPVUQVUBVUDVUGUPMZUQZVUPVUQYJZUYSUYPVVRUYPU
      VGUVDLMZUYSVVRUVDUVGLUYGUYTVRVSUYQVVHVVMVVTVVRRZUYRUVDXTZUYRVUTVVMVVMCNUV
      BVVOWQYDVVLVWAEUVDUVBUWHUVDQZVVIVVTVVKVVRUWHUVDUVGLVKVWCVVJVVQVWCVVBVUDVU
      GUPUWHUVDUAVIYEYBVMYFYGXJYIUYSVVRVVSUTZUYPUYQVUDHJZVUGHJZVWDUYRUYQVVHVWEV
      WBUVDHHUEZVOUYRVUTVWFVVGUVGHHUEVOVWEVWFPZVVRVVSUQZUQVVSVWHVVQVWIHUPYKVWHV
      VQVWIUTYLHVUDVUGUPYMYNYBVVSYOYPYGYQYRYSYTZUUAUUBUYSUYPVUNVUMQZUYQVVHVUTUY
      PVWKUTUYRVWBVVGDFUVDUVGLHWLDFWMUUCYGUUDUUEVUFVUDUVGXPUIZVUOXSUUFVUBVVHVUK
      VULUTZUYQVVHUYRUYPVWBUUGZVVHVWEVUCHJVWMVWGUVDHHUUHVUDVUCVUFVWLUUIUUJVOYRV
      WJUUKVUBUVBUULZVVHUVDVUEQHHUUMZVWNUVDUVBUUNUUOVUBVWOVUTUVGVUHQVWPUYRVUTUY
      QUYPVVGUUPUVGUVBUUNUUOUUQWHXJUURUUSUVEUVHACNUVDUVGBLWSUUTUVA $.
      $( [17-Sep-2013] $) $( [28-Apr-2013] $)
  $}

  ${
    $d A x y $.
    $( Corrolary of ~ nqereu : the function ` /Q ` acts as the identity on
       members of ` Q. ` .  (Contributed by Mario Carneiro, 6-May-2013.) $)
    nqeridNEW $p |- ( A e. Q. -> ( /Q ` A ) = A ) $=
      ( vx vy cv cerq cfv wceq cnq fveq2 id eqeq12d wfun wcel wbr cnpi cxp wf
      nqerf ffun ax-mp ceq cin elpqn enqer cmi dfenq2 ecopoprdmNEW erref2 syl
      wb brinxp mpancom mpbid df-erq breqi sylibr visset funbrfv mpsyl vtoclga
      ) BDZEFZVAGZAEFZAGBAHVAAGZVBVDVAAVAAEIVEJKELZVAHMZVAVAENZVCOOPZHEQVFRVIHE
      STVGVAVAUAVIHPUBZNZVHVGVAVAUANZVKVGVAVIMZVLVAUCZVAVIUAUDBCUAOUEBCUFUGUHUI
      VMVGVLVKUJVNVAVAVIHUAUKULUMVAVAEVJUNUOUPVAVAEBUQURUSUT $.
      $( [17-Sep-2013] $) $( [6-May-2013] $)
  $}

  ${
    $d A x y $.  $d B x $.
    $( Corrolary of ~ nqereu : if two fractions are both reduced and
       equivalent, then they are equal.  (Contributed by Mario Carneiro,
       6-May-2013.) $)
    enqeqNEW $p |- ( ( A e. Q. /\ B e. Q. /\ A ~Q B ) -> A = B ) $=
      ( vx vy cnq wcel ceq wbr w3a wa cv wmo wceq 3simpa cnpi cxp wreu elpqn
      3ad2ant2 nqereu wrex reu5 simprbi 3syl 3simpb simp2 enqer cmi dfenq2
      ecopoprdmNEW erref2 syl jca eleq1 breq1 anbi12d moi syl112anc ) AEFZBEFZA
      BGHZIZUSUTJCKZEFZVCBGHZJZCLZUSVAJZUTBBGHZJZABMUSUTVANVBBOOPZFZVECEQZVGUTU
      SVLVABRZSCBTVMVECEUAVGVECEUBUCUDUSUTVAUEVBUTVIUSUTVAUFUTUSVIVAUTVLVIVNBVK
      GUGCDGOUHCDUIUJUKULSUMVFVHVJCABEEVCAMVDUSVEVAVCAEUNVCABGUOUPVCBMVDUTVEVIV
      CBEUNVCBBGUOUPUQUR $.
      $( [17-Sep-2013] $) $( [28-Apr-2013] $)
  $}

  $( The function ` /Q ` acts as a substitute for equivalence classes, and it
     satisfies the fundamental requirement for equivalence representatives: the
     representatives are equal iff the members are equivalent.  (Contributed by
     Mario Carneiro, 6-May-2013.) $)
  nqereqNEW $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) ) ->
      ( A ~Q B <-> ( /Q ` A ) = ( /Q ` B ) ) ) $=
    ( cnpi cxp wcel wa ceq wbr cerq cfv wceq w3a cnq nqercl 3ad2ant1 3ad2ant2
    nqerrel fvex enqer wss wrel cltpq ccnv cun cdif df-enqNEW difss eqsstri
    relxp relss mp2 errsym syl simp3 errtr syl11anc enqeq syl111anc 3expia
    breq1 biimparc sylan syl2an expr impbid ) ACCDZEZBVFEZFABGHZAIJZBIJZKZVGVHV
    IVLVGVHVILZVJMEZVKMEZVJVKGHZVLVGVHVNVIANOVHVGVOVIBNPVMVJAGHZAVKGHZVPVGVHVQV
    IVGAVJGHZVQAQZAVJGAIRSGVFVFDZTWAUAGUAGWAUBUBUCUDZUEWAUFWAWBUGUHVFVFUIGWAUJU
    KZULUMOVMVIBVKGHZVRVGVHVIUNVHVGWDVIBQZPABVKGSWCUOUPVJAVKGSWCUOUPVJVKUQURUSV
    GVHVLVIVGVSVJBGHZVIVHVLFVTVHVKBGHZVLWFVHWDWGWEBVKGBIRSWCULUMVLWFWGVJVKBGUTV
    AVBAVJBGSWCUOVCVDVE $.
    $( [17-Sep-2013] $) $( [6-May-2013] $)

  ${
    $d A x y z $.  $d B x y z $.  $d C y z $.  $d D y z $.
    $( Addition of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 6-May-2013.) $)
    addpipq2NEW $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) ) ->
      ( A +pQ B ) = <. ( ( ( 1st ` A ) .N ( 2nd ` B ) ) +N
                         ( ( 1st ` B ) .N ( 2nd ` A ) ) ) ,
                         ( ( 2nd ` A ) .N ( 2nd ` B ) ) >. ) $=
      ( vx vy cnpi cxp cv c1st cfv c2nd cmi co cpli cop cplpq wceq fveq2
      opreq1d opreq2d opreq12d opeq12d df-plpqNEW opex ovmpt2 ) CDABEEFZUECGZHI
      ZDGZJIZKLZUHHIZUFJIZKLZMLZULUIKLZNAHIZBJIZKLZBHIZAJIZKLZMLZUTUQKLZNOUPUIK
      LZUKUTKLZMLZUTUIKLZNUFAPZUNVFUOVGVHUJVDUMVEMVHUGUPUIKUFAHQRVHULUTUKKUFAJQ
      ZSTVHULUTUIKVIRUAUHBPZVFVBVGVCVJVDURVEVAMVJUIUQUPKUHBJQZSVJUKUSUTKUHBHQRT
      VJUIUQUTKVKSUACDUBVBVCUCUD $.
      $( [17-Sep-2013] $) $( [8-May-2013] $)

    $( Addition of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    addpqnqNEW $p |- ( ( A e. Q. /\ B e. Q. ) ->
        ( A +Q B ) = ( /Q ` ( A +pQ B ) ) ) $=
      ( vx vy cnq wcel wa cop cplq cfv cplpq cerq co ccom cxp cres wceq df-plq
      fveq1i a1i opelxpi fvres syl cnpi wfn cv c1st c2nd cmi cpli df-plpqNEW
      opex fnoprab2a elpqn syl2an wfun wf nqerf ffun ax-mp fvco2 mp3an1
      sylancr 3eqtrd df-opr fveq2i 3eqtr4g ) AEFZBEFZGZABHZIJZVKKJZLJZABIMABKMZ
      LJVJVLVKLKNZEEOZPZJZVKVPJZVNVLVSQVJVKIVRRSTVJVKVQFVSVTQABEEUAVKVQVPUBUCVJ
      KUDUDOZWAOZUEZVKWBFZVTVNQZCDWAWACUFZUGJDUFZUHJZUIMWGUGJWFUHJZUIMUJMZWIWHU
      IMZHKCDUKWJWKULUMVHAWAFBWAFWDVIAUNBUNABWAWAUAUOLUPZWCWDWEWAELUQWLURWAELUS
      UTWBVKLKVAVBVCVDABIVEVOVMLABKVEVFVG $.
      $( [17-Sep-2013] $) $( [28-Aug-1995] $)

    $( Multiplication of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    mulpipq2NEW $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) ) ->
      ( A .pQ B ) = <. ( ( 1st ` A ) .N ( 1st ` B ) ) ,
                       ( ( 2nd ` A ) .N ( 2nd ` B ) ) >. ) $=
      ( vx vy cnpi cxp cv c1st cfv cmi co c2nd cop cmpq wceq fveq2 opreq1d
      opeq12d opreq2d df-mpqNEW opex ovmpt2 ) CDABEEFZUCCGZHIZDGZHIZJKZUDLIZUFL
      IZJKZMAHIZBHIZJKZALIZBLIZJKZMNULUGJKZUOUJJKZMUDAOZUHURUKUSUTUEULUGJUDAHPQ
      UTUIUOUJJUDALPQRUFBOZURUNUSUQVAUGUMULJUFBHPSVAUJUPUOJUFBLPSRCDTUNUQUAUB
      $.
      $( [17-Sep-2013] $) $( [8-May-2013] $)

    $( Multiplication of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    mulpqnqNEW $p |- ( ( A e. Q. /\ B e. Q. ) ->
        ( A .Q B ) = ( /Q ` ( A .pQ B ) ) ) $=
      ( vx vy cnq wcel wa cop cmq cfv cmpq cerq co ccom cxp cres wceq df-mq
      fveq1i a1i opelxpi fvres syl cnpi wfn cv c1st cmi c2nd df-mpqNEW opex
      fnoprab2a elpqn syl2an wfun wf nqerf ffun ax-mp fvco2 mp3an1 sylancr
      3eqtrd df-opr fveq2i 3eqtr4g ) AEFZBEFZGZABHZIJZVJKJZLJZABIMABKMZLJVIVKVJ
      LKNZEEOZPZJZVJVOJZVMVKVRQVIVJIVQRSTVIVJVPFVRVSQABEEUAVJVPVOUBUCVIKUDUDOZV
      TOZUEZVJWAFZVSVMQZCDVTVTCUFZUGJDUFZUGJUHMZWEUIJWFUIJUHMZHKCDUJWGWHUKULVGA
      VTFBVTFWCVHAUMBUMABVTVTUAUNLUOZWBWCWDVTELUPWIUQVTELURUSWAVJLKUTVAVBVCABIV
      DVNVLLABKVDVEVF $.
      $( [17-Sep-2013] $) $( [28-Aug-1995] $)
  $}

  ${
    $d x y $.
    $( Closure of addition on positive fractions.  (Contributed by Mario
       Carneiro, 8-May-2013.) $)
    addpqfNEW $p |- +pQ : ( ( N. X. N. ) X. ( N. X. N. ) ) --> ( N. X. N. ) $=
      ( vx vy cv c1st cfv c2nd cmi co cpli cop cnpi cxp wcel wral cplpq wf wa
      xp1st xp2nd mulclpi syl2an adantl adantr syl11anc addclpi opelxpi rgen2a
      df-plpqNEW foprab2a mpbi ) ACZDEZBCZFEZGHZUMDEZUKFEZGHZIHZUQUNGHZJZKKLZMZ
      BVBNAVBNVBVBLVBOPVCABVBUKVBMZUMVBMZQZUSKMZUTKMZVCVFUOKMZURKMZVGVDULKMUNKM
      ZVIVEUKKKRUMKKSZULUNTUAVFUPKMZUQKMZVJVEVMVDUMKKRUBVDVNVEUKKKSZUCUPUQTUDUO
      URUEUDVDVNVKVHVEVOVLUQUNTUAUSUTKKUFUDUGABVBVBVAVBOABUHUIUJ $.
      $( [17-Sep-2013] $) $( [29-Aug-1995] $)

    $( Closure of multiplication on positive fractions.  (Contributed by Mario
       Carneiro, 8-May-2013.) $)
    mulpqfNEW $p |- .pQ : ( ( N. X. N. ) X. ( N. X. N. ) ) --> ( N. X. N. ) $=
      ( vx vy cv c1st cfv cmi co c2nd cop cnpi cxp wcel wral cmpq wf wa xp1st
      mulclpi syl2an xp2nd opelxpi syl11anc rgen2a df-mpqNEW foprab2a mpbi ) AC
      ZDEZBCZDEZFGZUGHEZUIHEZFGZIZJJKZLZBUPMAUPMUPUPKUPNOUQABUPUGUPLZUIUPLZPUKJ
      LZUNJLZUQURUHJLUJJLUTUSUGJJQUIJJQUHUJRSURULJLUMJLVAUSUGJJTUIJJTULUMRSUKUN
      JJUAUBUCABUPUPUOUPNABUDUEUF $.
      $( [17-Sep-2013] $) $( [29-Aug-1995] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.
    $( Lemma for ~ adderpq . $)
    adderpqlemNEW $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) /\
        C e. ( N. X. N. ) ) ->
      ( A ~Q B <-> ( A +pQ C ) ~Q ( B +pQ C ) ) ) $=
      ( vx vy vz cnpi cxp wcel w3a c1st cfv c2nd cmi co cpli cop ceq wbr wceq
      cplpq wb xp1st 3ad2ant1 xp2nd 3ad2ant3 mulclpi syl11anc addclpi 3ad2ant2
      dfenq2 ecopopreqNEW syl22anc addpipq2 3adant2 3adant1 breq12d ecopopreq2
      3adant3 oprex mulcanpi mulcompi fvex cv visset mulasspi caopr4 eqtri
      opreq12i addcompi distrpi caoprdistrr 3eqtr4i 3eqtrri opreq1i 3eqtri
      3eqtr4ri eqeq12i addcanpi syl5rbbr 3bitr2d 3bitr4rd ) AGGHZIZBWCIZCWCIZJZ
      AKLZCMLZNOZCKLZAMLZNOZPOZWLWINOZQZBKLZWINOZWKBMLZNOZPOZWSWINOZQZRSZWNXBNO
      ZXAWONOZTZACUAOZBCUAOZRSABRSZWGWNGIZWOGIZXAGIZXBGIZXDXGUBWGWJGIZWMGIZXKWG
      WHGIZWIGIZXOWDWEXQWFAGGUCUDZWFWDXRWECGGUEUFZWHWIUGUHWGWKGIZWLGIZXPWFWDYAW
      ECGGUCUFZWDWEYBWFAGGUEUDZWKWLUGUHWJWMUIUHWGYBXRXLYDXTWLWIUGUHZWGWRGIZWTGI
      ZXMWGWQGIZXRYFWEWDYHWFBGGUCUJXTWQWIUGUHWGYAWSGIZYGYCWEWDYIWFBGGUEUJZWKWSU
      GUHZWRWTUIUHWGYIXRXNYJXTWSWIUGUHDEWNWOXAXBRGNDEUKZULUMWGXHWPXIXCRWDWFXHWP
      TWEACUNUOWEWFXIXCTWDBCUNUPUQWGXJWHWSNOZWQWLNOZTZWIWINOZYMNOZYPYNNOZTZXGWD
      WEXJYOUBWFDEABRGNYLURUSWGYPGIZYMGIZYSYOUBWGXRXRYTXTXTWIWIUGUHZWGXQYIUUAXS
      YJWHWSUGUHZYPYMYNWQWLNUTZVAUHXGWOWTNOZYQPOZUUEYRPOZTZWGYSUUFXEUUGXFYQUUEP
      OWJXBNOZWMXBNOZPOUUFXEYQUUIUUEUUJPYQYMYPNOUUIYPYMWIWINUTWHWSNUTVBDEFWHWSW
      IWINAKVCBMVCZCMVCZDVDZEVDZDVEEVEZVBZUUMUUNFVDZUUOFVEZVFZUULVGVHUUEWLWKNOZ
      WIWSNOZNOUUJDEFWLWIWKWSNAMVCZUULCKVCZUUPUUSUUKVGUUTWMUVAXBNWLWKUVBUVCVBWI
      WSUULUUKVBVIVHVIUUEYQWOWTNUTZYPYMNUTVJDEFWJWMXBPNWHWINUTWKWLNUTWSWINUTUUP
      UUMUUNUUQUUOUURVKVLVMWOXANOWOWRNOZUUEPOZXFUUGWOWRWTWQWINUTWKWSNUTVKXAWOWR
      WTPUTWLWINUTVBUUGYRUUEPOUVFUUEYRUVDYPYNNUTZVJYRUVEUUEPYRWIWIYNNOZNOZWOWQN
      OZWINOZUVEWIWIYNUULUUDVFUVIUVHWINOUVKWIUVHUULWIYNNUTVBUVHUVJWINUVJWLWIWQN
      OZNOUVLWLNOUVHWLWIWQUULBKVCZVFWLUVLUVBWIWQNUTVBWIWQWLUVMUVBVFVNVOVHWOWQWI
      UVMUULVFVPVOVHVQVRWGUUEGIZYQGIZUUHYSUBWGXLYGUVNYEYKWOWTUGUHWGYTUUAUVOUUBU
      UCYPYMUGUHUUEYQYRUVGVSUHVTWAWB $.
      $( [17-Sep-2013] $) $( [8-May-2013] $)

    $( Lemma for ~ mulerpq . $)
    mulerpqlemNEW $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) /\
        C e. ( N. X. N. ) ) ->
      ( A ~Q B <-> ( A .pQ C ) ~Q ( B .pQ C ) ) ) $=
      ( vx vy vz cnpi cxp wcel w3a c1st cfv cmi co c2nd cop ceq wbr wceq cmpq
      wb xp1st 3ad2ant1 3ad2ant3 mulclpi syl11anc xp2nd 3ad2ant2 dfenq2
      ecopopreqNEW syl22anc mulpipq2 3adant2 3adant1 breq12d ecopopreq2
      3adant3 oprex mulcanpi mulcompi fvex cv visset mulasspi caopr4 eqtri
      eqeq12i a1i 3bitr2d 3bitr4rd ) AGGHZIZBVKIZCVKIZJZAKLZCKLZMNZAOLZCOLZMNZP
      ZBKLZVQMNZBOLZVTMNZPZQRZVRWFMNZWDWAMNZSZACTNZBCTNZQRABQRZVOVRGIZWAGIZWDGI
      ZWFGIZWHWKUAVOVPGIZVQGIZWOVLVMWSVNAGGUBUCZVNVLWTVMCGGUBUDZVPVQUEUFVOVSGIZ
      VTGIZWPVLVMXCVNAGGUGUCVNVLXDVMCGGUGUDZVSVTUEUFVOWCGIZWTWQVMVLXFVNBGGUBUHX
      BWCVQUEUFVOWEGIZXDWRVMVLXGVNBGGUGUHZXEWEVTUEUFDEVRWAWDWFQGMDEUIZUJUKVOWLW
      BWMWGQVLVNWLWBSVMACULUMVMVNWMWGSVLBCULUNUOVOWNVPWEMNZWCVSMNZSZVQVTMNZXJMN
      ZXMXKMNZSZWKVLVMWNXLUAVNDEABQGMXIUPUQVOXMGIZXJGIZXPXLUAVOWTXDXQXBXEVQVTUE
      UFVOWSXGXRXAXHVPWEUEUFXMXJXKWCVSMURZUSUFXPWKUAVOXNWIXOWJXNXJXMMNWIXMXJVQV
      TMURZVPWEMURUTDEFVPWEVQVTMAKVABOVACKVAZDVBZEVBZDVCEVCZUTZYBYCFVBYDFVCVDZC
      OVAZVEVFXOXKXMMNWJXMXKXTXSUTDEFWCVSVQVTMBKVAAOVAYAYEYFYGVEVFVGVHVIVJ $.
      $( [17-Sep-2013] $) $( [8-May-2013] $)
  $}

  ${
    $d a b c x y $.  $d a b c A $.  $d b c B $.  $d c C $.
    $( Lemma for distributive law: cancellation of common factor.  (Contributed
       by Mario Carneiro, 8-May-2013.) $)
    mulcanenqNEW $p |- ( ( A e. N. /\ B e. N. /\ C e. N. ) ->
      <. ( A .N B ) , ( A .N C ) >. ~Q <. B , C >. ) $=
      ( va vb vc vx vy cv cmi co cop ceq wbr cnpi wceq opreq1 opeq12d breq1d
      opreq2 opeq1d opeq1 breq12d opeq2d opeq2 wcel w3a visset mulcompi
      opreq1i mulasspi eqtri wa wb mulclpi 3adant3 3adant2 3simpc dfenq2
      ecopopreqNEW syl21anc mpbiri vtocl3ga ) DIZEIZJKZVDFIZJKZLZVEVGLZMNZAVEJK
      ZAVGJKZLZVJMNABJKZVMLZBVGLZMNVOACJKZLZBCLZMNDEFABCOOOVDAPZVIVNVJMWAVFVLVH
      VMVDAVEJQVDAVGJQRSVEBPZVNVPVJVQMWBVLVOVMVEBAJTUAVEBVGUBUCVGCPZVPVSVQVTMWC
      VMVRVOVGCAJTUDVGCBUEUCVDOUFZVEOUFZVGOUFZUGZVKVFVGJKZVEVHJKZPZWHVEVDJKZVGJ
      KWIVFWKVGJVDVEDUHZEUHUIUJVEVDVGWLFUHUKULWGVFOUFZVHOUFZWEWFUMVKWJUNWDWEWMW
      FVDVEUOUPWDWFWNWEVDVGUOUQWDWEWFURGHVFVHVEVGMOJGHUSUTVAVBVC $.
      $( [17-Sep-2013] $) $( [2-Sep-1995] $)
  $}

  ${
    $d r s t x y z $.
    $( 'Less than' is a strict ordering on positive fractions.  (Contributed by
       Mario Carneiro, 4-May-2013.) $)
    ltsonqNEW $p |- <Q Or Q. $=
      ( vx vy vz vr vs vt cnq cltq cv wcel w3a wbr wceq wo wn wb wa wi c1st
      cfv c2nd cmi co clti cnpi cxp elpqn adantr xp1st syl adantl xp2nd
      mulclpi syl11anc wor ltsopi sotric mpan ordpinq fveq2 eqcomd opreq12d
      ceq dfenq2 ecopopreq2 syl2an enqeq 3expia sylbird impbid2 ancoms orbi12d
      notbid 3bitr4d 3adant3 3ad2ant3 oprex ltmpi 3syl bitrd 3adant1 3ad2ant1
      anbi12d fvex visset mulcompi mulasspi caopr13 breq12i ltrelpi sotri
      syl5eqbrr sylan2b syl6bi 3adant2 3ad2ant2 sylibrd jca so ) ABCGHAIZGJZBIZ
      GJZCIZGJZKZWTXBHLZWTXBMZXBWTHLZNZOZPZXGXBXDHLZQZWTXDHLZRXAXCXLXEXAXCQZWTS
      TZXBUATZUBUCZXBSTZWTUATZUBUCZUDLZXSYBMZYBXSUDLZNZOZXGXKXPXSUEJZYBUEJZYCYG
      PZXPXQUEJZXRUEJZYHXPWTUEUEUFZJZYKXAYNXCWTUGZUHZWTUEUEUIUJXPXBYMJZYLXCYQXA
      XBUGZUKZXBUEUEULZUJXQXRUMUNXPXTUEJZYAUEJZYIXPYQUUAYSXBUEUEUIUJXPYNUUBYPWT
      UEUEULZUJXTYAUMUNUEUDUOYHYIQYJUPUEXSYBUDUQURUNWTXBUSZXPXJYFXPXHYDXIYEXPXH
      YDXHXQXTXRYAUBWTXBSUTXHYAXRWTXBUAUTVAVBXPYDWTXBVCLZXHXAYNYQUUEYDPXCYOYRDE
      WTXBVCUEUBDEVDVEVFXAXCUUEXHWTXBVGVHVIVJXCXAXIYEPXBWTUSVKVLVMVNVOXFXNXRXQX
      DUATZUBUCZUBUCZXRXDSTZYAUBUCZUBUCZUDLZXOXFXNUUFXSUBUCZUUFYBUBUCZUDLZYAXTU
      UFUBUCZUBUCZYAUUIXRUBUCZUBUCZUDLZQUULXFXGUUOXMUUTXFXGYCUUOXAXCXGYCPXEUUDV
      OXFXDYMJZUUFUEJYCUUOPXEXAUVAXCXDUGVPXDUEUEULXSYBUUFXQXRUBVQXTYAUBVQVRVSVT
      XFXMUUPUURUDLZUUTXCXEXMUVBPXAXBXDUSWAXFYNUUBUVBUUTPXAXCYNXEYOWBUUCUUPUURY
      AXTUUFUBVQUUIXRUBVQVRVSVTWCUUTUUOUUNUUKUDLZUULUUQUUNUUSUUKUDDEFYAXTUUFUBW
      TUAWDZXBSWDXDUAWDZDIZEIZDWEEWEZWFZUVFUVGFIUVHFWEWGZWHDEFYAUUIXRUBUVDXDSWD
      XBUAWDZUVIUVJWHWIUUOUVCQUUHUUMUUKUDDEFUUFXQXRUBUVEWTSWDUVKUVIUVJWHUUMUUNU
      UKUDUEUPWJWKWLWMWNXFXOUUGUUJUDLZUULXAXEXOUVLPXCWTXDUSWOXFYQYLUVLUULPXCXAY
      QXEYRWPYTUUGUUJXRXQUUFUBVQUUIYAUBVQVRVSVTWQWRWS $.
      $( [18-Sep-2013] $) $( [19-Feb-1996] $)
  $}

  ${
    $d x y z w v u f $.
    $( Define pre-addition on signed reals.  This is a "temporary" set used in
       the construction of complex numbers ~ df-c , and is intended to be used
       only by the construction.  From Proposition 9-4.1 of [Gleason]
       p. 126. $)
    df-plprNEW $a |- +pR = ( x e. ( P. X. P. ) , y e. ( P. X. P. ) |->
         <. ( ( 1st ` x ) +P. ( 1st ` y ) ) ,
            ( ( 2nd ` x ) +P. ( 2nd ` y ) ) >. ) $.

    $( Define pre-multiplication on signed reals.  This is a "temporary" set
       used in the construction of complex numbers ~ df-c , and is intended to
       be used only by the construction.  From Proposition 9-4.1 of [Gleason]
       p. 126. $)
    df-mprNEW $a |- .pR = ( x e. ( P. X. P. ) , y e. ( P. X. P. ) |->
       <. ( ( ( 1st ` x ) .P. ( 1st ` y ) ) +P.
            ( ( 2nd ` x ) .P. ( 2nd ` y ) ) ) ,
          ( ( ( 1st ` x ) .P. ( 2nd ` y ) ) +P.
            ( ( 2nd ` x ) .P. ( 1st ` y ) ) ) >. ) $.

    $( Define equivalence relation for signed reals.  This is a "temporary" set
       used in the construction of complex numbers ~ df-c , and is intended to
       be used only by the construction.  From Proposition 9-4.1 of [Gleason]
       p. 126. $)
    df-enrNEW $a |- ~R = { <. x , y >. |
     ( ( x e. ( P. X. P. ) /\ y e. ( P. X. P. ) ) /\
       ( ( 1st ` x ) +P. ( 2nd ` y ) ) = ( ( 1st ` y ) +P. ( 2nd ` x ) ) ) } $.

    $( Define addition on signed reals.  This is a "temporary" set used in the
       construction of complex numbers ~ df-c , and is intended to be used only
       by the construction.  From Proposition 9-4.3 of [Gleason] p. 126. $)
    df-plrNEW $a |- +R = ( x e. R. , y e. R. |->
      ( iota z E. w E. v ( x = [ w ] ~R /\ y = [ v ] ~R /\
               z = [ ( w +pR v ) ] ~R ) ) ) $.

    $( Define multiplication on signed reals.  This is a "temporary" set used
       in the construction of complex numbers ~ df-c , and is intended to be
       used only by the construction.  From Proposition 9-4.3 of [Gleason]
       p. 126. $)
    df-mrNEW $a |- .R = ( x e. R. , y e. R. |->
      ( iota z E. w E. v ( x = [ w ] ~R /\ y = [ v ] ~R /\
               z = [ ( w .pR v ) ] ~R ) ) ) $.

    $( Define ordering relation on signed reals.  This is a "temporary" set
       used in the construction of complex numbers ~ df-c , and is intended to
       be used only by the construction.  From Proposition 9-4.4 of [Gleason]
       p. 127. $)
    df-ltrNEW $a |- <R = { <. x , y >. | ( ( x e. R. /\ y e. R. ) /\
       E. z E. w ( x = [ z ] ~R /\ y = [ w ] ~R /\
    ( ( 1st ` z ) +P. ( 2nd ` w ) ) <P ( ( 1st ` w ) +P. ( 2nd ` z ) ) ) ) } $.
  $}

  ${
    $d x y z $.
    $( The equivalence relation for signed reals is an equivalence relation.
       Proposition 9-4.1 of [Gleason] p. 126. $)
    enrerNEW $p |- Er ~R $=
      ( vx vy vz cer cnp cpp df-enrNEW cv addclpr visset addcompr addasspr
      addcanpr ecopoprerNEW ) ABCDEFABGAHZBHZIOPAJBJZKOPCHZQCJZLOPRQSMN $.
      $( [17-Sep-2013] $) $( [3-Sep-1995] $)
  $}

  ${
    $d x y $.
    $( The equivalence relation for signed reals exists. $)
    enrexNEW $p |- ~R e. _V $=
      ( vx vy cer cnp cxp npex xpex cv wcel wa c1st cfv c2nd cpp co wceq copab
      df-enrNEW opabssxp eqsstri ssexi ) CDDEZUBEZUBUBDDFFGZUDGCAHZUBIBHZUBIJUE
      KLUFMLNOUFKLUEMLNOPZJABQUCABRUGABUBUBSTUA $.
      $( [17-Sep-2013] $) $( [25-Jul-1995] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.
    $( Lemma showing compatibility of addition. $)
    addcmpblnrNEW $p |- ( ( ( A e. ( P. X. P. ) /\ B e. ( P. X. P. ) ) /\
                         ( C e. ( P. X. P. ) /\ D e. ( P. X. P. ) ) ) ->
                ( ( A ~R B /\ C ~R D ) -> ( A +pR C ) ~R ( B +pR D ) ) ) $=
      ( vx vy vz cnp cxp wcel wa cer wbr cplpr co c1st cfv c2nd cpp wceq
      df-enrNEW ecopopreq2 bi2anan9 opreq12 fvex cv visset addcompr addasspr
      caopr4 3eqtr4g wb cop fveq2 opreq1d opeq12d opreq2d df-plprNEW opex
      ovmpt2 fveq2d oprex op1st syl6eq op2nd syl2an syl2anc eqeq12d an4s
      syl5ibr sylbid xp1st addclpr xp2nd opelxpi syl11anc eqeltrd sylibrd ) AHH
      IZJZBVSJZKZCVSJZDVSJZKZKZABLMZCDLMZKZACNOZPQZBDNOZRQZSOZWLPQZWJRQZSOZTZWJ
      WLLMZWFWIAPQZBRQZSOZBPQZARQZSOZTZCPQZDRQZSOZDPQZCRQZSOZTZKZWRWBWGXFWEWHXM
      EFABLHSEFUAZUBEFCDLHSXOUBUCXNWRWFWTXGSOZXAXHSOZSOZXCXJSOZXDXKSOZSOZTZXNXB
      XISOXEXLSOXRYAXBXEXIXLSUDEFGWTXGXAXHSAPUECPUEBRUEEUFZFUFZEUGFUGZUHZYCYDGU
      FYEGUGUIZDRUEUJEFGXCXJXDXKSBPUEDPUEARUEYFYGCRUEUJUKVTWCWAWDWRYBULVTWCKZWA
      WDKZKWNXRWQYAYHWKXPTWMXQTWNXRTYIYHWKXPXTUMZPQXPYHWJYJPEFACVSVSYCPQZYDPQZS
      OZYCRQZYDRQZSOZUMZYJNWTYLSOZXDYOSOZUMYCATZYMYRYPYSYTYKWTYLSYCAPUNUOYTYNXD
      YOSYCARUNUOUPYDCTZYRXPYSXTUUAYLXGWTSYDCPUNUQUUAYOXKXDSYDCRUNUQUPEFURZXPXT
      USUTZVAXPXTWTXGSVBZVCVDYIWMXSXQUMZRQXQYIWLUUEREFBDVSVSYQUUENXCYLSOZXAYOSO
      ZUMYCBTZYMUUFYPUUGUUHYKXCYLSYCBPUNUOUUHYNXAYOSYCBRUNUOUPYDDTZUUFXSUUGXQUU
      IYLXJXCSYDDPUNUQUUIYOXHXASYDDRUNUQUPUUBXSXQUSUTZVAXSXQXCXJSVBZXAXHSVBVEVD
      WKXPWMXQSUDVFYIWOXSTWPXTTWQYATYHYIWOUUEPQXSYIWLUUEPUUJVAXSXQUUKVCVDYHWPYJ
      RQXTYHWJYJRUUCVAXPXTUUDXDXKSVBVEVDWOXSWPXTSUDVGVHVIVJVKVTWCWAWDWSWRULZYHW
      JVSJWLVSJUULYIYHWJYJVSUUCYHXPHJZXTHJZYJVSJVTWTHJXGHJUUMWCAHHVLCHHVLWTXGVM
      VFVTXDHJXKHJUUNWCAHHVNCHHVNXDXKVMVFXPXTHHVOVPVQYIWLUUEVSUUJYIXSHJZXQHJZUU
      EVSJWAXCHJXJHJUUOWDBHHVLDHHVLXCXJVMVFWAXAHJXHHJUUPWDBHHVNDHHVNXAXHVMVFXSX
      QHHVOVPVQEFWJWLLHSXOUBVFVIVR $.
      $( [18-Sep-2013] $) $( [3-Sep-1995] $)

    ${
      $d x y z E $.  $d x y z F $.  $d x y z G $.  $d x y z H $.
      cmpblnrNEW.1 $e |- A e. _V $.
      cmpblnrNEW.2 $e |- B e. _V $.
      cmpblnrNEW.3 $e |- C e. _V $.
      cmpblnrNEW.4 $e |- D e. _V $.
      cmpblnrNEW.5 $e |- E e. _V $.
      cmpblnrNEW.6 $e |- F e. _V $.
      cmpblnrNEW.7 $e |- G e. _V $.
      cmpblnrNEW.8 $e |- H e. _V $.
      $( Lemma used in lemma showing compatibility of multiplication. $)
      mulcmpblnrlemNEW $p |- ( ( ( A +P. D ) = ( C +P. B ) /\
                              ( E +P. H ) = ( G +P. F ) ) ->
          ( ( D .P. E ) +P. ( ( ( A .P. E ) +P. ( B .P. F ) ) +P.
                              ( ( C .P. H ) +P. ( D .P. G ) ) ) ) =
          ( ( D .P. E ) +P. ( ( ( C .P. G ) +P. ( D .P. H ) ) +P.
                              ( ( A .P. F ) +P. ( B .P. E ) ) ) ) ) $=
        ( vx vy vz cpp co wceq wa cmp oprex addasspr opreq2i eqtr4i opreq1 cv
        visset mulcompr distrpr caoprdistrr 3eqtr3g opreq1d addcompr opreq1i
        eqtr3i caopr32 3eqtr4g opreq2 syl6eq sylan9eq opreq2d sylan9eqr
        3eqtr4a caopr4 caopr411 eqtri 3eqtri ) ADTUAZCBTUAZUBZEHTUAZGFTUAZUBZUC
        ZDEUDUAZAEUDUAZCHUDUAZTUAZTUAZBFUDUAZDGUDUAZTUAZTUAZCGUDUAZBEUDUAZTUAZA
        FUDUAZDHUDUAZTUAZVSTUAZTUAZVSVTWDTUAWAWETUATUAZTUAZVSWHWLTUAZWKWITUAZTU
        AZTUAZVRWJCFUDUAZTUAZWFTUAZWJXBWDTUAZWETUAZTUAZWGWOXDWJXBWFTUAZTUAXGWJX
        BWFCFUDUEZWDWETUEZUFXFXHWJTXBWDWEBFUDUEZDGUDUEZUFUGUHVRWCXCWFTVNVQWCCEU
        DUAZWATUAZWITUAZXCVNVTVSTUAZWATUAZXMWITUAZWATUAWCXOVNXPXRWATVNVLEUDUAVM
        EUDUAXPXRVLVMEUDUIQRSADETUDILMQUJZRUJZQUKZRUKZULZXSXTSUJZYBSUKZUMZUNQRS
        CBETUDKJMYCYFUNUOUPVSVTTUAZWATUAWCXQVSVTWAAEUDUEZCHUDUEZUFYGXPWATVSVTDE
        UDUEZYHUQURUSQRSXMWAWITCEUDUEYIBEUDUEZXSXTYAYBUQZXSXTYDYBYEUFZUTVAVQXOW
        HXBTUAZWITUAXCVQXNYNWITVQCVOUDUACVPUDUAXNYNVOVPCUDVBCEHMPUMCGFONUMUOUPQ
        RSWHXBWITCGUDUEZXIYKYLYMUTVCVDUPVRWNXFWJTVQVNWNWKDFUDUAZTUAZWETUAZXFVQW
        KWLVSTUAZTUAWKYPWETUAZTUAWNYRVQYSYTWKTVQDVOUDUAZDVPUDUAZYSYTVOVPDUDVBYS
        VSWLTUAUUAWLVSDHUDUEZYJUQDEHMPUMUHYTWEYPTUAUUBYPWEDFUDUEZXLUQDGFONUMUHV
        AVEWKWLVSUUCYJUFWKYPWEUUDXLUFVAVNYQXEWETVNVLFUDUAVMFUDUAYQXEVLVMFUDUIQR
        SADFTUDILNYCYFUNQRSCBFTUDKJNYCYFUNUOUPVFVEVGWQVSWBWFTUAZTUAWGWPUUEVSTQR
        SVTWDWAWETYHXKYIYLYMXLVHUGVSWBWFVTWATUEXJUFUHXAVSWJWMTUAZTUAUUFVSTUAWOW
        TUUFVSTWTWSWRTUAUUFWRWSWHWLTUEWKWITUEUQQRSWKWIWHWLTAFUDUEYKYOYLYMUUCVIV
        JUGVSUUFYJWJWMTUEUQWJWMVSWKWLTUEYJUFVKVA $.
        $( [18-Sep-2013] $) $( [4-Sep-1995] $)
    $}

    $( Lemma showing compatibility of multiplication. $)
    mulcmpblnrNEW $p |- ( ( ( A e. ( P. X. P. ) /\ B e. ( P. X. P. ) ) /\
                         ( C e. ( P. X. P. ) /\ D e. ( P. X. P. ) ) ) ->
                ( ( A ~R B /\ C ~R D ) -> ( A .pR C ) ~R ( B .pR D ) ) ) $=
      ( vx vy cnp cxp wcel wa cer wbr cmpr co c1st cfv c2nd cpp wceq df-enrNEW
      ecopopreq2 bi2anan9 cmp fvex mulcmpblnrlemNEW wi xp2nd adantl xp1st
      adantr mulclpr syl2an addclpr syl11anc an4s oprex addcanpr syl5 wb cop
      cv fveq2 opreq1d opreq12d opeq12d opreq2d df-mprNEW opex ovmpt2 fveq2d
      op1st syl6eq op2nd opreq12 syl2anc eqeq12d sylibrd sylbid opelxpi
      eqeltrd ) AGGHZIZBWAIZJZCWAIZDWAIZJZJZABKLZCDKLZJZACMNZOPZBDMNZQPZRNZWNOP
      ZWLQPZRNZSZWLWNKLZWHWKAOPZBQPZRNBOPZAQPZRNSZCOPZDQPZRNDOPZCQPZRNSZJZWTWDW
      IXFWGWJXKEFABKGREFTZUAEFCDKGRXMUAUBWHXLXBXGUCNZXEXJUCNZRNZXDXHUCNZXCXIUCN
      ZRNZRNZXDXIUCNZXCXHUCNZRNZXBXJUCNZXEXGUCNZRNZRNZSZWTXLXCXGUCNZXTRNYIYGRNS
      ZWHYHXBXEXDXCXGXJXIXHAOUDAQUDBOUDBQUDCOUDCQUDDOUDDQUDUEWHYIGIZXTGIZYJYHUF
      WDXCGIZXGGIZYKWGWCYMWBBGGUGZUHWEYNWFCGGUIZUJXCXGUKULWBWEWCWFYLWBWEJZXPGIZ
      XSGIZYLWCWFJZYQXNGIZXOGIZYRWBXBGIZYNUUAWEAGGUIZYPXBXGUKULWBXEGIZXJGIZUUBW
      EAGGUGZCGGUGZXEXJUKULXNXOUMUNZYTXQGIZXRGIZYSWCXDGIZXHGIZUUJWFBGGUIZDGGUGZ
      XDXHUKULWCYMXIGIZUUKWFYODGGUIZXCXIUKULXQXRUMUNZXPXSUMULUOYIXTYGXPXSRUPYCY
      FRUPUQUNURWBWEWCWFWTYHUSYQYTJWPXTWSYGYQWMXPSWOXSSWPXTSYTYQWMXPYFUTZOPXPYQ
      WLUUSOEFACWAWAEVAZOPZFVAZOPZUCNZUUTQPZUVBQPZUCNZRNZUVAUVFUCNZUVEUVCUCNZRN
      ZUTZUUSMXBUVCUCNZXEUVFUCNZRNZXBUVFUCNZXEUVCUCNZRNZUTUUTASZUVHUVOUVKUVRUVS
      UVDUVMUVGUVNRUVSUVAXBUVCUCUUTAOVBZVCUVSUVEXEUVFUCUUTAQVBZVCVDUVSUVIUVPUVJ
      UVQRUVSUVAXBUVFUCUVTVCUVSUVEXEUVCUCUWAVCVDVEUVBCSZUVOXPUVRYFUWBUVMXNUVNXO
      RUWBUVCXGXBUCUVBCOVBZVFUWBUVFXJXEUCUVBCQVBZVFVDUWBUVPYDUVQYERUWBUVFXJXBUC
      UWDVFUWBUVCXGXEUCUWCVFVDVEEFVGZXPYFVHVIZVJXPYFXNXORUPZVKVLYTWOYCXSUTZQPXS
      YTWNUWHQEFBDWAWAUVLUWHMXDUVCUCNZXCUVFUCNZRNZXDUVFUCNZXCUVCUCNZRNZUTUUTBSZ
      UVHUWKUVKUWNUWOUVDUWIUVGUWJRUWOUVAXDUVCUCUUTBOVBZVCUWOUVEXCUVFUCUUTBQVBZV
      CVDUWOUVIUWLUVJUWMRUWOUVAXDUVFUCUWPVCUWOUVEXCUVCUCUWQVCVDVEUVBDSZUWKYCUWN
      XSUWRUWIYAUWJYBRUWRUVCXIXDUCUVBDOVBZVFUWRUVFXHXCUCUVBDQVBZVFVDUWRUWLXQUWM
      XRRUWRUVFXHXDUCUWTVFUWRUVCXIXCUCUWSVFVDVEUWEYCXSVHVIZVJYCXSYAYBRUPZXQXRRU
      PVMVLWMXPWOXSRVNULYTWQYCSWRYFSWSYGSYQYTWQUWHOPYCYTWNUWHOUXAVJYCXSUXBVKVLY
      QWRUUSQPYFYQWLUUSQUWFVJXPYFUWGYDYERUPVMVLWQYCWRYFRVNVOVPUOVQVRWBWEWCWFXAW
      TUSZYQWLWAIWNWAIUXCYTYQWLUUSWAUWFYQYRYFGIZUUSWAIUUIYQYDGIZYEGIZUXDWBUUCUU
      FUXEWEUUDUUHXBXJUKULWBUUEYNUXFWEUUGYPXEXGUKULYDYEUMUNXPYFGGVSUNVTYTWNUWHW
      AUXAYTYCGIZYSUWHWAIYTYAGIZYBGIZUXGWCUULUUPUXHWFUUNUUQXDXIUKULWCYMUUMUXIWF
      YOUUOXCXHUKULYAYBUMUNUURYCXSGGVSUNVTEFWLWNKGRXMUAULUOVQ $.
      $( [18-Sep-2013] $) $( [5-Sep-1995] $)
  $}

  ${
    $d a b c d x y z A $.  $d a b c d x y z B $.  $d b c d y z C $.
    $d b c d y z D $.
    $( Addition of signed reals in terms of positive reals. $)
    addsrprNEW $p |- ( ( ( A e. P. /\ B e. P. ) /\ ( C e. P. /\ D e. P. ) ) ->
                ( [ <. A , B >. ] ~R +R [ <. C , D >. ] ~R ) =
                   [ <. ( A +P. C ) , ( B +P. D ) >. ] ~R ) $=
      ( vx vy vz vb vd va vc cnp wcel wa cop cer cec cplr co c1st cfv cpp c2nd
      cnr cplpr cv opex enrex enrer df-enrNEW df-plprNEW wceq fveq2 opreq1d
      opeq12d opreq2d df-plrNEW df-nr addcmpblnrNEW oprecNEW op1stg opreq12
      syl2an ad2ant2r op2ndg eceq2 syl eqtrd ) ALMZBLMZNZCLMZDLMZNZNZABOZPQCDOZ
      PQRSVPTUAZVQTUAZUBSZVPUCUAZVQUCUAZUBSZOZPQZACUBSZBDUBSZOZPQZEFGABCDUDPLRU
      EWDEUFZTUAZFUFZTUAZUBSZWJUCUAZWLUCUAZUBSZOVRWMUBSZWAWPUBSZOUBHIJKVTWCUGUH
      UIEFUJEFUKWJVPULZWNWRWQWSWTWKVRWMUBWJVPTUMUNWTWOWAWPUBWJVPUCUMUNUOWLVQULZ
      WRVTWSWCXAWMVSVRUBWLVQTUMUPXAWPWBWAUBWLVQUCUMUPUOEFGHIUQURJUFHUFKUFIUFUSU
      TVOWDWHULWEWIULVOVTWFWCWGVIVLVTWFULZVJVMVIVRAULVSCULXBVLABLVACDLVAVRAVSCU
      BVBVCVDVKWABULWBDULWCWGULVNABLLVECDLLVEWABWBDUBVBVCUOWDWHPVFVGVH $.
      $( [17-Sep-2013] $) $( [3-Sep-1995] $)

    $( Multiplication of signed reals in terms of positive reals. $)
    mulsrprNEW $p |- ( ( ( A e. P. /\ B e. P. ) /\ ( C e. P. /\ D e. P. ) ) ->
                ( [ <. A , B >. ] ~R .R [ <. C , D >. ] ~R ) =
                  [ <. ( ( A .P. C ) +P. ( B .P. D ) ) ,
                       ( ( A .P. D ) +P. ( B .P. C ) ) >. ] ~R ) $=
      ( vx vy vz vb vd va vc cnp wcel wa cop cer cec cmr co c1st cfv cmp c2nd
      cpp cnr cmpr cv opex enrex enrer df-enrNEW df-mprNEW wceq fveq2 opreq1d
      opreq12d opeq12d opreq2d df-mrNEW df-nr mulcmpblnrNEW oprecNEW op1stg
      opreq12 syl2an ad2ant2r op2ndg adantr eceq2 syl eqtrd ) ALMZBLMZNZCLMZDLM
      ZNZNZABOZPQCDOZPQRSVSTUAZVTTUAZUBSZVSUCUAZVTUCUAZUBSZUDSZWAWEUBSZWDWBUBSZ
      UDSZOZPQZACUBSZBDUBSZUDSZADUBSZBCUBSZUDSZOZPQZEFGABCDUEPLRUFWKEUGZTUAZFUG
      ZTUAZUBSZXAUCUAZXCUCUAZUBSZUDSZXBXGUBSZXFXDUBSZUDSZOWAXDUBSZWDXGUBSZUDSZW
      AXGUBSZWDXDUBSZUDSZOUDHIJKWGWJUHUIUJEFUKEFULXAVSUMZXIXOXLXRXSXEXMXHXNUDXS
      XBWAXDUBXAVSTUNZUOXSXFWDXGUBXAVSUCUNZUOUPXSXJXPXKXQUDXSXBWAXGUBXTUOXSXFWD
      XDUBYAUOUPUQXCVTUMZXOWGXRWJYBXMWCXNWFUDYBXDWBWAUBXCVTTUNZURYBXGWEWDUBXCVT
      UCUNZURUPYBXPWHXQWIUDYBXGWEWAUBYDURYBXDWBWDUBYCURUPUQEFGHIUSUTJUGHUGKUGIU
      GVAVBVRWKWSUMWLWTUMVRWGWOWJWRVRWCWMWFWNUDVLVOWCWMUMZVMVPVLWAAUMZWBCUMZYEV
      OABLVCZCDLVCZWAAWBCUBVDVEVFVNWDBUMZWEDUMZWFWNUMVQABLLVGZCDLLVGZWDBWEDUBVD
      VEUPVRWHWPWIWQUDVNYFYKWHWPUMVQVLYFVMYHVHYMWAAWEDUBVDVEVNYJYGWIWQUMVQYLVOY
      GVPYIVHWDBWBCUBVDVEUPUQWKWSPVIVJVK $.
      $( [17-Sep-2013] $) $( [3-Sep-1995] $)
  $}

  $(
  @{
    @( Ordering of signed reals in terms of positive reals. @)
    ltsrpr2 @p |- ( ( A e. ( P. X. P. ) /\ B e. ( P. X. P. ) ) ->
     ( [ A ] ~R <R [ B ] ~R <->
      ( ( 1st ` A ) +P. ( 2nd ` B ) ) <P ( ( 1st ` B ) +P. ( 2nd ` A ) ) ) ) @=
      ? @.
      @( [20-Feb-1996] @)
  @}

  @{
    @d A x y z w v u f @.  @d B x y z w v u f @.  @d C x y z w v u f @.
    @d D x y z w v u f @.
    ltsrprNEW.1 @e |- A e. _V @.
    ltsrprNEW.2 @e |- B e. _V @.
    ltsrprNEW.3 @e |- C e. _V @.
    ltsrprNEW.4 @e |- D e. _V @.
    @( Ordering of signed reals in terms of positive reals. @)
    ltsrprNEW @p |- ( [ <. A , B >. ] ~R <R [ <. C , D >. ] ~R <->
                          ( A +P. D ) <P ( C +P. B ) ) @=
      ( vz vu vw vv vx vy vf cltp cltr cer cpp cnp cnr enrex dmenr df-nr
      ltrelsr ltrelpr 0npr dmplp cv co wbr enrer df-ltr wcel wa cop cec wceq
      wb addclpr ad2ant2lr anim12i ancoms an4s enreceq eqcom syl6bb bi2anan9
      opreq12 visset addcompr addasspr caopr4 3eqtr4g syl6bi oprex ltapr
      caoprord3 ex sylsyld brecop brecop2 ) ABCDPQRSTUAUBFGHUCUDUEUFUGUHIUIZJUI
      ZSUJZKUIZLUIZSUJZPUKZADSUJZBCSUJZPUKZMNIKLJABCDQRTUAUBULUCUDMNIKLJUMWCTUN
      ZWFTUNZUOZATUNZBTUNZUOZUOZWGTUNZWDTUNZUOZCTUNZDTUNZUOZUOZUOZWKTUNZWHTUNZU
      OZWCWFUPRUQABUPRUQURZWGWDUPRUQCDUPRUQURZUOZWEWKSUJZWHWJSUJZURZWIWLUSZWOXB
      WRXEXJWRXEUOZWOXBUOZXJXRXHXSXIWQXCXHWPXDBCUTVAWNWTXIWMXAWFWGUTVAVBVCVDXGX
      MWCBSUJZWFASUJZURZWDCSUJZWGDSUJZURZUOZXPWSXKYBXFXLYEWCWFABVEXFXLYDYCURYEW
      GWDCDVEYDYCVFVGVHYFXTYCSUJYAYDSUJXNXOXTYAYCYDSVIMNOWCWDBCSIVJJVJFMUIZNUIZ
      MVJZNVJZVKZYGYHOUIZYJOVJVLZGVMMNOWFWGADSKVJLVJEYKYMHVMVNVOXJXPXQMNOWEWKWH
      WJPTSWCWDSVPBCSVPYGYHYLYIYJVQWFWGSVPYKADSVPVRVSVTWAWB @.
      @( [20-Feb-1996] @)
  @}
  $)

  ${
    $d x y z w v $.
    $( Domain of addition on signed reals. $)
    dmaddsrNEW $p |- dom +R = ( R. X. R. ) $=
      ( vx vy vw vv vz cplr cnr cxp wfn cdm wceq cv cer cec cplpr co w3a wex
      cio df-plrNEW iotaex fnoprab2a fndm ax-mp ) FGGHZIFJUEKABGGALCLZMNKBLDLZM
      NKELUFUGOPMNKQDRCRZESFABECDTUHEUAUBUEFUCUD $.
      $( [17-Sep-2013] $) $( [25-Aug-1995] $)

    $( Domain of multiplication on signed reals. $)
    dmmulsrNEW $p |- dom .R = ( R. X. R. ) $=
      ( vx vy vw vv vz cmr cnr cxp wfn cdm wceq cv cer cec cmpr co w3a wex cio
      df-mrNEW iotaex fnoprab2a fndm ax-mp ) FGGHZIFJUEKABGGALCLZMNKBLDLZMNKELU
      FUGOPMNKQDRCRZESFABECDTUHEUAUBUEFUCUD $.
      $( [17-Sep-2013] $) $( [25-Aug-1995] $)
  $}

  ${
    $d x y $.
    $( The empty set is not a signed real. $)
    0nsrNEW $p |- -. (/) e. R. $=
      ( vx vy c0 cnr wcel cnp cxp cer cqs cpp df-enrNEW ecopoprdmNEW 0nelqs
      df-nr eleq2i mtbir ) CDECFFGZHIZEQHABHFJABKLMDRCNOP $.
      $( [18-Sep-2013] $) $( [25-Aug-1995] $)
  $}

  ${
    $d x y A $.  $d x y z w v $.
    $( 1 is an identity element for multiplication. $)
    1idsrNEW $p |- ( A e. R. -> ( A .R 1R ) = A ) $=
      ( vx vy vz vw cv cop cer cec c1r cmr co wceq cnp cnr df-nr opreq1 id
      eqeq12d wcel wa c1p cpp df-1r opreq2i cmp 1pr addclpr mp2an mulsrpr
      mpanr12 wbr elisseti distrpr 1idpr opreq1d syl5req opreq2d syl5eq
      opreq12 syl2an oprex addasspr visset eqcomi 3eqtr3g wb mulclpr mpan2 jca
      df-enrNEW ecopopreqNEW mpdan mpbird opex enrer erthi syl eqtr4d ecoptocl
      ) BFZCFZGZHIZJKLZWDMAJKLZAMBCANNHOPWDAMZWEWFWDAWDAJKQWGRSWANTZWBNTZUAZWEW
      DUBUBUCLZUBGHIZKLZWDJWLWDKUDUEWJWMWAWKUFLZWBUBUFLZUCLZWAUBUFLZWBWKUFLZUCL
      ZGZHIZWDWJWKNTZUBNTZWMXAMXCXCXBUGUGUBUBUHUIZUGWAWBWKUBUJUKWJWCWTHULZWDXAM
      WJXEWAWSUCLZWPWBUCLZMZWJWAWQUCLZWRUCLZWNWOWBUCLZUCLZXFXGWHXIWNMWRXKMXJXLM
      WIWHWNWQWQUCLXIWAUBUBUBNUGUMZXMUNWHWQWAWQUCWAUOUPUQWIWRWOWOUCLXKWBUBUBXMX
      MUNWIWOWBWOUCWBUOURUSXIWNWRXKUCUTVAWAWQWRWAUBUFVBWBWKUFVBVCXGXLWNWOWBWBUB
      UFVBCVDVCVEVFWJWPNTZWSNTZUAXEXHVGWJXNXOWHWNNTZWONTZXNWIWHXBXPXDWAWKVHVIWI
      XCXQUGWBUBVHVIWNWOUHVAWHWQNTZWRNTZXOWIWHXCXRUGWAUBVHVIWIXBXSXDWBWKVHVIWQW
      RUHVAVJDEWAWBWPWSHNUCDEVKVLVMVNWCWTHWAWBVOWPWSVOVPVQVRVSUSVT $.
      $( [18-Sep-2013] $) $( [2-May-1996] $)
  $}

  ${
    $d x y z A $.  $d x y z w v a b $.
    recexsrlemNEW.1 $e |- A e. _V $.
    $( The reciprocal of a positive signed real exists.  Part of Proposition
       9-4.3 of [Gleason] p. 126. $)
    recexsrlemNEW $p |- ( 0R <R A -> E. x e. R. ( A .R x ) = 1R ) $=
      ( vy vz vw vv va vb cnr wcel c0r cltr wbr cv cmr co c1r wceq wrex
      ltrelsr brel simprd cop cer cec wi cnp df-nr breq2 opreq1 eqeq1d rexbidv
      imbi12d cpp wa cltp visset gt0srpr ltexpri sylbi cmp c1p recexpr 1pr
      addclpr mpan2 enrex ecopqsi sylancl ad2antlr jctir anim2i adantr mulsrpr
      syl eqcomd mulcompr distrpr caoprdistrr opreq2 syl5eq sylan9eqr opreq1d
      oprex elisseti addcompr addasspr caopr32 syl6eq opreq1i eqtri opreq2i
      caopr12 3eqtr4g wb mulclpr sylan2 syl2an an32s anassrs jca mp2an pm3.2i
      df-enrNEW ecopopreqNEW syl5ibr imp opex enrer erthi eqtrd df-1r syl6eqr
      rcla4ev syl11anc exp43 r19.23adv syl5 ecoptocl mpcom ) BJKZLBMNZBAOZPQZRS
      ZAJTZYCLJKYBLBJJMCUAUBUCLDOZEOZUDUEUFZMNZYJYDPQZRSZAJTZUGYCYGUGDEBUHUHUEJ
      UIYJBSZYKYCYNYGYJBLMUJYOYMYFAJYOYLYERYJBYDPUKULUMUNYKYIFOZUOQZYHSZFUHTZYH
      UHKZYIUHKZUPZYNYKYIYHUQNYSYHYIDURZEURZUSFYIYHUUCUTVAUUBYRYNFUHYPUHKYPGOZV
      BQZVCSZGUHTUUBYRYNUGZGYPVDUUBUUGUUHGUHUUBUUEUHKZUUGYRYNUUBUUIUPZUUGYRUPZU
      PZUUEVCUOQZVCUDUEUFZJKZYJUUNPQZRSZYNUUIUUOUUBUUKUUIUUMUHKZVCUHKZUUOUUIUUS
      UURVEUUEVCVFVGZVEUHUUMVCUEJVHUIVIVJVKUULUUPVCVCUOQZVCUDZUEUFZRUULUUPYHUUM
      VBQZYIVCVBQZUOQZYHVCVBQZYIUUMVBQZUOQZUDZUEUFZUVCUULUUBUURUUSUPZUPZUUPUVKS
      UUJUVMUUKUUIUVLUUBUUIUURUUSUUTVEVLVMVNYHYIUUMVCVOVPUULUVJUVBUENZUVKUVCSUU
      JUUKUVNUUKUVNUUJUVFVCUOQZUVAUVIUOQZSZUUKYHUUEVBQZUVGUVEUOQZUOQZVCUOQZYIUU
      EVBQZUVSUOQZUVAUOQZUVOUVPUUKUWAUWCVCUOQZVCUOQUWDUUKUVTUWEVCUOUUKUVTUWBVCU
      OQZUVSUOQUWEUUKUVRUWFUVSUOYRUUGUVRYQUUEVBQZUWFYRUWGUVRYQYHUUEVBUKVQUUGUWG
      UWBUUFUOQUWFHIAYIYPUUEUOVBUUDFURGURZHOZIOZHURZIURZVRUWIUWJYDUWLAURZVSVTUU
      FVCUWBUOWAWBWCWDHIAUWBVCUVSUOYIUUEVBWEZVCUHVEWFZUVGUVEUOWEUWIUWJUWKUWLWGZ
      UWIUWJYDUWLUWMWHZWIWJWDUWCVCVCUWOUWOWHWJUVFUVTVCUOUVFUVRUVGUOQZUVEUOQUVTU
      VDUWRUVEUOYHUUEVCUWHUWOVSWKUVRUVGUVEYHVCVBWEZYIVCVBWEZWHWLWKUVPUVIUVAUOQU
      WDUVAUVIVCVCUOWEUVGUVHUOWEWGUVIUWCUVAUOUVIUVGUWBUVEUOQZUOQUWCUVHUXAUVGUOY
      IUUEVCUWHUWOVSWMHIAUVGUWBUVEUOUWSUWNUWTUWPUWQWNWLWKWLWOUUJUVFUHKZUVIUHKZU
      PUVAUHKZUUSUPUVNUVQWPUUJUXBUXCYTUUIUUAUXBYTUUIUPUVDUHKZUVEUHKZUXBUUAUUIYT
      UURUXEUUTYHUUMWQWRUUAUUSUXFVEYIVCWQVGUVDUVEVFWSWTYTUUAUUIUXCYTUVGUHKZUVHU
      HKZUXCUUAUUIUPYTUUSUXGVEYHVCWQVGUUIUUAUURUXHUUTYIUUMWQWRUVGUVHVFWSXAXBUXD
      UUSUUSUUSUXDVEVEVCVCVFXCVEXDAFUVFUVIUVAVCUEUHUOAFXEXFVJXGXHUVJUVBUEUVFUVI
      XIUVAVCXIXJXKVPXLXMXNYMUUQAUUNJYDUUNSYLUUPRYDUUNYJPWAULXOXPXQXRXSXRXSXTYA
      $.
      $( [17-Sep-2013] $) $( [15-May-1996] $)
  $}

  $(
  @{
    @d x y z w v A @.  @d x y z C @.
    map2psrprNEW.1 @e |- A e. _V @.
    map2psrprNEW.2 @e |- C e. R. @.
    @( Equivalence for positive signed real.  (Contributed by Mario Carneiro,
       15-Jun-2013.) @)
    map2psrprNEW @p |- ( ( C +R -1R ) <R A <->
            E. x e. P. ( C +R [ <. x , 1P >. ] ~R ) = A ) @=
      ( vy vz vw vv cltr cplr cm1r wcel wceq c1p cnp cpp cnr wbr co r19.23aiva
      ecopopreq sylan9eqr elisseti addcompr addasssr mappsrpr addasspr ecoptocl
      reximdva addcomsr syl6bir ltrelsr sylbird mulclsr reximdv ltexpri biimpac
      3bitr4i 3eqtr3i syl5bbr rexbidv imbi12d opreq2i addclsr opreq1i visset cv
      df-m1r impbii syl5bb simprd df-enr breq2d ltsrpr breq1i syl5bi breq2i cer
      eqeq1i mpanl2 ancoms syl5eq opreq2 oprex ax-mp breq2 0idsr bitri df-nr wi
      eqeq2 sylbi mp2an pn0sr eqtri erthi mpcom ltasr enrer ltapr wrex cltp c0r
      opex mpan brel syld syl5 1pr cec cop cmr m1r syl wb wa ex 0r ) CLKTZBJSZC
      AVHZOXLZVSXKZKTZBNZAPXBZBRMZYAYGYAXTRMYHXTBRRJDUMXGVLYHYALCLXMTZBKTZJSZYG
      YKXTCYJKTZJSZYHYACRMZYKYMXPELYJCLRXNUDYIBKWEWSWFYHYLBXTJYHYLBXDKTZBCYIKTZ
      BKTXDBKTYLYOYPXDBKYNYPXDNECWOWFVFCYIBCLXMWEDUFXDBXDRXSUDDUKUTBWHWCZVNVKYH
      YKYDYJNZAPXBZYGYHYJRMZYKYSWKZYIRMZYHYTYNLRMUUBEXNCLUOWNYIBVEXFLFVHZGVHZXL
      ZVSXKZJSZYDUUFNZAPXBZWKUUAFGYJPPVSRWJUUFYJNZUUGYKUUIYSUUFYJLJWGUUJUUHYRAP
      UUFYJYDWLVBVCUUGUUDYBQTZUUCOQTZNZAPXBZUUCPMUUDPMXQZUUIUUGUUDUULXCSZUUNUUG
      OOOQTZXLVSXKZUUFJSZUUPLUURUUFJVIVPOUUDQTZUUQUUCQTZXCSUUTOUULQTZXCSZUUSUUP
      UVAUVBUUTXCUVAOOUUCQTZQTUVBOOUUCOPXJUDZFVGZUHUVDUULOQOUUCUVEUVFUEVDWPVROU
      UQUUCUUDUVEOOQWEUVFGVGZVOOPMZUUPUVCXPXJUUDUULOUVGUUCOQWEZXAWFUSWIAUUDUULU
      VIUQWMUUOUUMUUHAPUUMYBUUDQTZUULNZUUOYBPMZXQUUHUUKUVJUULUUDYBUVGAVGZUEVTUV
      LUUOUVKUUHWKUVLUUOXQUVKYCUUEVSSZUUHUVLUVHUUOUVNUVKXPXJHIYBOUUCUUDVSPQHIVM
      UBWAYCUUEVSYBOXEUUCUUDXEWTWQULWBVQUJXIUIXOYHYRYFAPYHYRYFYRYHYEYLBYDYJCKWD
      YQUCXRUPXHUNWRYFYAAPYFUVLYAUVLXTYEJSYFYAYBCUVMEUGYEBXTJWGVAURUAVJ @.
      @( [18-Sep-2013] @) @( [17-May-1996] @)
  @}
  $)

  ${
    $d x y $.
    $( Define addition over complex numbers. $)
    df-plusNEW $a |- + = ( x e. CC , y e. CC |->
                  <. ( ( 1st ` x ) +R ( 1st ` y ) ) ,
                     ( ( 2nd ` x ) +R ( 2nd ` y ) ) >. ) $.

    $( Define multiplication over complex numbers. $)
    df-mulNEW $a |- x. = ( x e. CC , y e. CC |->
               <. ( ( ( 1st ` x ) .R ( 1st ` y ) ) +R
           ( -1R .R ( ( 2nd ` x ) .R ( 2nd ` y ) ) ) ) ,
                  ( ( ( 2nd ` x ) .R ( 1st ` y ) ) +R
                    ( ( 1st ` x ) .R ( 2nd ` y ) ) ) >. ) $.
  $}

