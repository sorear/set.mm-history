$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                                  Models of ZF
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c AxExt $. $( The Axiom of Extensionality $)
  $c AxRep $. $( The Axiom Scheme of Replacement $)
  $c AxPow $. $( The Axiom of Power Sets $)
  $c AxUn $. $( The Axiom of Unions $)
  $c AxReg $. $( The Axiom of Regularity $)
  $c AxInf $. $( The Axiom of Infinity $)
  $c ZF $. $( The set of models of ZF $)

  $( The Axiom of Extensionality. $)
  cgze $a class AxExt $.

  $( The Axiom Scheme of Replacement. $)
  cgzr $a class AxRep $.

  $( The Axiom of Power Sets. $)
  cgzp $a class AxPow $.

  $( The Axiom of Unions. $)
  cgzu $a class AxUn $.

  $( The Axiom of Regularity. $)
  cgzg $a class AxReg $.

  $( The Axiom of Infinity. $)
  cgzi $a class AxInf $.

  $( The set of models of ZF. $)
  cgzf $a class ZF $.

  $( The Godel-set version of the Axiom of Extensionality. $)
  df-gzext $a |- AxExt =
    ( A.g 2o ( ( 2o e.g (/) ) <->g ( 2o e.g 1o ) ) ->g ( (/) =g 1o ) ) $.

  $( The Godel-set version of the Axiom Scheme of Replacement.  Since this is a
     scheme and not a single axiom, it manifests as a function on wffs, each
     giving rise to a different axiom. $)
  df-gzrep $a |- AxRep = ( u e. ( Fmla ` om ) |->
    ( A.g 3o E.g 1o A.g 2o ( A.g 1o u ->g ( 2o =g 1o ) ) ->g A.g 1o A.g 2o
      ( ( 2o e.g 1o ) <->g E.g 3o ( ( 3o e.g (/) ) /\g A.g 1o u ) ) ) ) $.

  $( The Godel-set version of the Axiom of Power Sets. $)
  df-gzpow $a |- AxPow =
    E.g 1o A.g 2o ( A.g 1o ( ( 1o e.g 2o ) <->g ( 1o e.g (/) ) ) ->g
                    ( 2o e.g 1o ) ) $.

  $( The Godel-set version of the Axiom of Unions. $)
  df-gzun $a |- AxUn =
    E.g 1o A.g 2o ( E.g 1o ( ( 2o e.g 1o ) /\g ( 1o e.g (/) ) ) ->g
                    ( 2o e.g 1o ) ) $.

  $( The Godel-set version of the Axiom of Regularity. $)
  df-gzreg $a |- AxReg = ( E.g 1o ( 1o e.g (/) ) ->g
      E.g 1o ( ( 1o e.g (/) ) /\g
               A.g 2o ( ( 2o e.g 1o ) ->g -.g ( 2o e.g (/) ) ) ) ) $.

  $( The Godel-set version of the Axiom of Infinity. $)
  df-gzinf $a |- AxInf = E.g 1o ( ( (/) e.g 1o ) /\g A.g 2o ( ( 2o e.g 1o ) ->g
      E.g (/) ( ( 2o e.g (/) ) /\g ( (/) e.g 1o ) ) ) ) $.

  $( Define the class of all (transitive) models of ZF. $)
  df-gzf $a |- ZF = { m | ( ( Tr m /\ m |= AxExt /\ m |= AxPow ) /\
                            ( m |= AxUn /\ m |= AxReg /\ m |= AxInf ) /\
                            A. u e. ( Fmla ` om ) m |= ( AxRep ` u ) ) } $.

$( (End of Mario Carneiro's mathbox.) $)


