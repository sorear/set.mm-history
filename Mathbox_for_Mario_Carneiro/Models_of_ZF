$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Models of ZF
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)
  $( Introduce new constant symbols. $)
  $c AxExt $. $( The Axiom of Extensionality $)
  $c AxRep $. $( The Axiom Scheme of Replacement $)
  $c AxPow $. $( The Axiom of Power Sets $)
  $c AxUn $. $( The Axiom of Unions $)
  $c AxReg $. $( The Axiom of Regularity $)
  $c AxInf $. $( The Axiom of Infinity $)
  $c ZF $. $( The set of models of ZF $)

  $( The Axiom of Extensionality. $)
  cgzext $a class AxExt $.

  $( The Axiom Scheme of Replacement. $)
  cgzrep $a class AxRep $.

  $( The Axiom of Power Sets. $)
  cgzpow $a class AxPow $.

  $( The Axiom of Unions. $)
  cgzun $a class AxUn $.

  $( The Axiom of Regularity. $)
  cgzreg $a class AxReg $.

  $( The Axiom of Infinity. $)
  cgzinf $a class AxInf $.

  $( The set of models of ZF. $)
  cgzf $a class ZF $.

  $( The Godel-set version of the Axiom of Extensionality. $)
  df-gzext $a |- AxExt =
    ( A.w 2o ( 2o e.w (/) <->w 2o e.w 1o ) ->w (/) =w 1o ) $.

  $( The Godel-set version of the Axiom Scheme of Replacement.  Since this is a
     scheme and not a single axiom, it manifests as a function on wffs, each
     giving rise to a different axiom. $)
  df-gzrep $a |- AxRep = ( u e. ( Fmla ` om ) |->
    ( A.w 3o E.w 1o A.w 2o ( A.w 1o u ->w 2o =w 1o ) ->w A.w 1o A.w 2o
      ( 2o e.w 1o <->w E.w 3o ( 3o e.w (/) /\w A.w 1o u ) ) ) ) $.

  $( The Godel-set version of the Axiom of Power Sets. $)
  df-gzpow $a |- AxPow =
    E.w 1o A.w 2o ( A.w 1o ( 1o e.w 2o <->w 1o e.w (/) ) ->w 2o e.w 1o ) $.

  $( The Godel-set version of the Axiom of Unions. $)
  df-gzun $a |- AxUn =
    E.w 1o A.w 2o ( E.w 1o ( 2o e.w 1o /\w 1o e.w (/) ) ->w 2o e.w 1o ) $.

  $( The Godel-set version of the Axiom of Regularity. $)
  df-gzreg $a |- AxReg = ( E.w 1o 1o e.w (/) ->w
      E.w 1o ( 1o e.w (/) /\w A.w 2o ( 2o e.w 1o ->w -.w 2o e.w (/) ) ) ) $.

  $( The Godel-set version of the Axiom of Infinity. $)
  df-gzinf $a |- AxInf = E.w 1o ( (/) e.w 1o /\w A.w 2o ( 2o e.w 1o ->w
      E.w (/) ( 2o e.w (/) /\w (/) e.w 1o ) ) ) $.

  $( Define the class of all (transitive) models of ZF. $)
  df-gzf $a |- ZF = { m | ( ( Tr m /\ m |= AxExt /\ m |= AxPow ) /\
                            ( m |= AxUn /\ m |= AxReg /\ m |= AxInf ) /\
                            A. u e. ( Fmla ` om ) m |= ( AxRep ` u ) ) } $.

$( (End of Mario Carneiro's mathbox.) $)


