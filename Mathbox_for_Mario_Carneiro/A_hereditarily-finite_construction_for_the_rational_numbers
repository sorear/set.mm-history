$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         A hereditarily-finite construction for the rational numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c /Q $. $( Positive fraction equivalence class $)
  $c <pQ $. $( Ordering on pre-fractions $)

  $( Positive fraction equivalence class. $)
  cerq $a class /Q $.
  $( Ordering on pre-fractions. $)
  cltpq $a class <pQ $.

  ${
    $d x y z w v u f $.
    $( Define pre-addition on positive fractions.  This is a "temporary" set
       used in the construction of complex numbers ~ df-c , and is intended to
       be used only by the construction.  This "pre-addition" operation works
       works directly with ordered pairs of integers.  The actual positive
       fraction addition ` +Q ` ( ~ df-plq ) works with the equivalence classes
       of these ordered pairs determined by the equivalence relation ` ~Q `
       ( ~ df-enq ).  (Analogous remarks apply to the other "pre-" operations
       in the complex number construction that follows.)  From Proposition
       9-2.3 of [Gleason] p. 117. $)
    df-plpqNEW $a |- +pQ = { <. <. x , y >. , z >. | ( ( x e. ( N. X. N. ) /\
         y e. ( N. X. N. ) ) /\ z = <. ( ( ( 1st ` x ) .N ( 2nd ` y ) ) +N
           ( ( 1st ` y ) .N ( 2nd ` x ) ) ) ,
           ( ( 2nd ` x ) .N ( 2nd ` y ) ) >. ) } $.

    $( Define pre-multiplication on positive fractions.  This is a "temporary"
       set used in the construction of complex numbers ~ df-c , and is intended
       to be used only by the construction.  From Proposition 9-2.4 of
       [Gleason] p. 119. $)
    df-mpqNEW $a |- .pQ = { <. <. x , y >. , z >. | ( ( x e. ( N. X. N. ) /\
         y e. ( N. X. N. ) ) /\ z = <. ( ( 1st ` x ) .N ( 1st ` y ) ) ,
           ( ( 2nd ` x ) .N ( 2nd ` y ) ) >. ) } $.

    $( Define class of positive fractions.  This is a "temporary" set used in
       the construction of complex numbers ~ df-c , and is intended to be used
       only by the construction.  This definition replaces the older and more
       conventional ` Q. = ( ( N. X. N. ) /. ~Q ) ` which defines fractions by
       the set of all numerator-denominator pairs that are equivalent to each
       other (i.e. ` 1 / 2 = { <. 1 , 2 >. , <. 2 , 4 >. , ... } ` ) with a
       definition that uses only the "lowest-terms" fraction, so that
       ` 1 / 2 = 2 / 4 = <. 1 , 2 >. ` .  Without needing to define the GCD, we
       find this representative of the equivalence class by minimizing the
       denominator (which is a positive integer and hence well-ordered), which
       works because no two equivalent fractions can have the same numerator
       and different denominator.

       The upside of this definition is that each fraction is now a finite set,
       so we can still work with them without the axiom of infinity. $)
    df-nqNEW $a |- Q. = { x e. ( N. X. N. ) | A. y e. ( N. X. N. )
      ( x ~Q y -> -. ( 2nd ` y ) <N ( 2nd ` x ) ) } $.

    $( Define a convenience function that "reduces" a fraction to lowest
       terms.  Note that in this form, it is not obviously a function; we prove
       this in ~ nqerf . $)
    df-erqNEW $a |- /Q = ( ~Q i^i ( ( N. X. N. ) X. Q. ) ) $.

    $( Define addition on positive fractions.  This is a "temporary" set used
       in the construction of complex numbers ~ df-c , and is intended to be
       used only by the construction.  From Proposition 9-2.3 of [Gleason]
       p. 117. $)
    df-plqNEW $a |- +Q = ( ( /Q o. +pQ ) |` ( Q. X. Q. ) ) $.

    $( Define multiplication on positive fractions.  This is a "temporary" set
       used in the construction of complex numbers ~ df-c , and is intended to
       be used only by the construction.  From Proposition 9-2.4 of [Gleason]
       p. 119. $)
    df-mqNEW $a |- .Q = ( ( /Q o. .pQ ) |` ( Q. X. Q. ) ) $.

    $( Define reciprocal on positive fractions.  It means the same thing as one
       divided by the argument (although we don't define full division since we
       will never need it).  This is a "temporary" set used in the construction
       of complex numbers ~ df-c , and is intended to be used only by the
       construction.  From Proposition 9-2.5 of [Gleason] p. 119, who uses an
       asterisk to denote this unary operation. $)
    df-rqNEW $a |- *Q = { <. x , y >. | ( x .Q y ) = 1Q } $.

    $( Define ordering relation on positive fractions.  This is a "temporary"
       set used in the construction of complex numbers ~ df-c , and is intended
       to be used only by the construction.  Similar to Definition 5 of
       [Suppes] p. 162. $)
    df-ltpqNEW $a |- <pQ = { <. x , y >. |
      ( ( x e. ( N. X. N. ) /\ y e. ( N. X. N. ) ) /\
        ( ( 1st ` x ) .N ( 2nd ` y ) ) <N ( ( 1st ` y ) .N ( 2nd ` x ) ) ) } $.

    $( Define ordering relation on positive fractions.  This is a "temporary"
       set used in the construction of complex numbers ~ df-c , and is intended
       to be used only by the construction.  Similar to Definition 5 of
       [Suppes] p. 162. $)
    df-ltnqNEW $a |- <Q = ( <pQ i^i ( Q. X. Q. ) ) $.

    $( Define positive fraction constant 1.  This is a "temporary" set used in
       the construction of complex numbers ~ df-c , and is intended to be used
       only by the construction.  From Proposition 9-2.2 of [Gleason]
       p. 117. $)
    df-1nq $a |- 1Q = <. 1o , 1o >. $.
  $}

  ${
    $( Equivalence relation for positive fractions in terms of positive
       integers.  (Contributed by Mario Carneiro, 8-May-2013.) $)
    enqbreq2 $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) ) ->
          ( A ~Q B <-> ( ( 1st ` A ) .N ( 2nd ` B ) ) =
                       ( ( 1st ` B ) .N ( 2nd ` A ) ) ) ) $=
      ( cnpi cxp wcel wa ceq wbr c1st cfv c2nd cop cmi co wceq wrel relxp
      1st2nd mpan breqan12d wb xp1st xp2nd jca enqbreq syl2an fvex mulcompi
      eqeq2i a1i 3bitrd ) ACCDZEZBULEZFZABGHAIJZAKJZLZBIJZBKJZLZGHZUPUTMNZUQUSM
      NZOZVCUSUQMNZOZUMUNAURBVAGULPZUMAUROCCQZAULRSVHUNBVAOVIBULRSTUMUPCEZUQCEZ
      FUSCEZUTCEZFVBVEUAUNUMVJVKACCUBACCUCUDUNVLVMBCCUBBCCUCUDUPUQUSUTUEUFVEVGU
      AUOVDVFVCUQUSAKUGBIUGUHUIUJUK $.
      $( [8-May-2013] $) $( [27-Aug-1995] $)
  $}

  ${
    $d x y $.
    $( The class of positive fractions exists.  (Contributed by Mario Carneiro,
       27-Apr-2013.) $)
    nqexNEW $p |- Q. e. _V $=
      ( vy vx cnq cv ceq wbr c2nd cfv clti wn wi cnpi cxp wral crab cvv
      df-nqNEW niex xpex rabex eqeltri ) CADZBDZEFUCGHUBGHIFJKBLLMZNZAUDOPABQUE
      AUDLLRRSTUA $.
      $( [27-Apr-2013] $) $( [16-Aug-1995] $)

    $( The empty set is not a positive fraction.  (Contributed by Mario
       Carneiro, 27-Apr-2013.) $)
    0nnq $p |- -. (/) e. Q. $=
      ( vy vx c0 cnq wcel cnpi cxp 0nelxp cv ceq wbr c2nd cfv clti wn wi wral
      crab df-nqNEW ssrab2 eqsstri sseli mto ) CDECFFGZEFFHDUDCDAIZBIZJKUFLMUEL
      MNKOPBUDQZAUDRUDABSUGAUDTUAUBUC $.
      $( [27-Apr-2013] $) $( [24-Aug-1995] $)

    $( Each positive fraction is an ordered pair of positive integers (the
       numerator and denominator, in "lowest terms".  (Contributed by Mario
       Carneiro, 28-Apr-2013.) $)
    elpqn $p |- ( A e. Q. -> A e. ( N. X. N. ) ) $=
      ( vy vx cnq cnpi cxp cv ceq wbr c2nd cfv clti wn wi wral crab df-nqNEW
      ssrab2 eqsstri sseli ) DEEFZADBGZCGZHIUCJKUBJKLIMNCUAOZBUAPUABCQUDBUARST
      $.
      $( [28-Apr-2013] $)

    $( Positive fraction 'less than' is a relation on positive fractions.
       (Contributed by Mario Carneiro, 27-Apr-2013.) $)
    ltrelnq $p |- <Q C_ ( Q. X. Q. ) $=
      ( cltq cltpq cnq cxp cin df-ltnqNEW inss2 eqsstri ) ABCCDZEIFBIGH $.
      $( [27-Apr-2013] $) $( [14-Feb-1996] $)
  $}

  ${
    $d A x y $.
    $( The representatives of positive integers as positive fractions.
       (Contributed by Mario Carneiro, 6-May-2013.) $)
    pinq $p |- ( A e. N. -> <. A , 1o >. e. Q. ) $=
      ( vx vy cnpi wcel c1o cop cv ceq wbr c2nd cfv clti wn wi cxp wral crab
      cnq 1pi opelxpi mpan2 nlt1pi cvv wceq elisseti op2ndg breq2d mtbiri a1d
      r19.21aiv breq1 fveq2 notbid imbi12d ralbidv elrab sylanbrc df-nqNEW
      syl6eleqr ) ADEZAFGZBHZCHZIJZVDKLZVCKLZMJZNZOZCDDPZQZBVKRZSVAVBVKEZVBVDIJ
      ZVFVBKLZMJZNZOZCVKQZVBVMEVAFDEVNTAFDDUAUBVAVSCVKVAVSVDVKEVAVRVOVAVQVFFMJV
      FUCVAVPFVFMVAFUDEVPFUEFDTUFAFDUDUGUBUHUIUJUJUKVLVTBVBVKVCVBUEZVJVSCVKWAVE
      VOVIVRVCVBVDIULWAVHVQWAVGVPVFMVCVBKUMUHUNUOUPUQURBCUSUT $.
      $( [6-May-2013] $) $( [29-Oct-1995] $)
  $}

  ${
    $d x y $.
    $( The positive fraction 'one'.  (Contributed by Mario Carneiro,
       28-Apr-2013.) $)
    1nq $p |- 1Q e. Q. $=
      ( c1q c1o cop cnq df-1nq cnpi wcel 1pi pinq ax-mp eqeltri ) ABBCZDEBFGLDG
      HBIJK $.
      $( [28-Apr-2013] $) $( [29-Oct-1995] $)
  $}

  ${
    $d A a b x y $.  $d B b x y $.  $d a b c d m x z $.  $d m x y z $.
    $( There is a unique element of ` Q. ` equivalent to each element of
       ` N. X. N. ` .  (Contributed by Mario Carneiro, 28-Apr-2013.) $)
    nqereu $p |- ( A e. ( N. X. N. ) -> E! x e. Q. x ~Q A ) $=
      ( vy va vb vm vd vc vz cnpi cxp wcel cv ceq wbr cnq wrex wa weq wi wral
      wreu cop wceq elxp2 wel con0 csuc pion suceloni syl visset sucid eleq2
      rcla4ev sylancl adantl elequ2 imbi1d 2ralbidv opeq1 breq2d rexbidv
      imbi2d elequ1 opeq2 imbi12d cbvral2v ralbii c2nd cfv clti wn rexnal
      pm4.63 wb xp2nd ltpiord ancoms sylan2 adantll anbi2d syl5bb rexbidva
      syl5bbr w3a c1st xp1st rcla4cv ralbidv eleq1 syl6 imp syl5 mpdi 3imp
      wrel relxp 1st2nd mpan 3ad2ant2 mpbird opex enqer ersym ertr expcom
      reximdv syl5com 3expia com23 imp3a r19.23adva ex com3r syl6bi com13 cmi
      co mulcompi enqbreq anidms mpbiri opelxpi breq1 fveq2 op2nd syl6eq
      notbid df-nqNEW elrab2 simplbi2 sylsyld com12 a1dd pm2.61d2 r19.21aivv
      sylbir a1i tfis2 ra4 r19.23adv mpd breq2 syl5ibrcom r19.23aivv sylbi
      anbi12d elpqn ad2antrr fvex simprbi breq1d rcla4va syl2an wo rabeq2i
      ad2antlr wor ltsopi sotric notnot syl6bbr syl11anc mpbid ord mt3d
      opreq2d syl5eq breqan12d jca bitrd biimpa eqtrd mulcanpi sylc opeq12d
      sylancr 3eqtr4d rgen2a vtoclg reu4 sylanbrc ) BJJKZLZAMZBNOZAPQZUWGCMZBNO
      ZRZACSZTZCPUAAPUAZUWGAPUBUWEBDMZEMZUCZUDZEJQDJQUWHDEBJJUEUWRUWHDEJJUWOJLZ
      UWPJLZRZUWHUWRUWFUWQNOZAPQZUXAECUFZCUGQZUXCUWTUXEUWSUWTUWPUHZUGLZUWPUXFLZ
      UXEUWTUWPUGLUXGUWPUIUWPUJUKUWPEULZUMUXDUXHCUXFUGUWIUXFUWPUNUOUPUQUXAUXDUX
      CCUGUWIUGLZUXAUXDUXCTZUXJUWSUWTUXKUXJUWSUXKEJUAZUWTUXKTUXJUXLDJUAZUWSUXLT
      UXMEFUFZUXCTZEJUADJUAZCFCFSZUXKUXODEJJUXQUXDUXNUXCCFEURUSUTUXPFUWIUAZUXMT
      UXJUXRGFUFZUWFHMZGMZUCZNOZAPQZTZGJUAHJUAZFUWIUAZUXMUYFUXPFUWIUYEUXOUXSUWF
      UWOUYAUCZNOZAPQZTHGDEJJHDSZUYDUYJUXSUYKUYCUYIAPUYKUYBUYHUWFNUXTUWOUYAVAVB
      VCVDGESZUXSUXNUYJUXCGEFVEUYLUYIUXBAPUYLUYHUWQUWFNUYAUWPUWOVFVBVCVGVHVIUYG
      UXKDEJJUYGUWQIMZNOZUYMVJVKZUWPVLOZVMZTZIUWDUAZUXAUXKTUXAUYSVMZUYGUXKUXAUY
      TUYNUYOUWPLZRZIUWDQZUYGUXKTUYTUYRVMZIUWDQUXAVUCUYRIUWDVNUXAVUDVUBIUWDVUDU
      YNUYPRUXAUYMUWDLZRZVUBUYNUYPVOVUFUYPVUAUYNUWTVUEUYPVUAVPZUWSVUEUWTUYOJLZV
      UGUYMJJVQZVUHUWTVUGUYOUWPVRVSVTWAWBWCWDWEUYGUXDVUCUXCUYGUXDVUCUXCTUYGUXDR
      ZVUBUXCIUWDVUJVUERZUYNVUAUXCVUKVUAUYNUXCVUJVUEVUAUYNUXCTVUJVUEVUAWFZUWFUY
      MNOZAPQZUYNUXCVULVUNUWFUYMWGVKZUYOUCZNOZAPQZVUJVUEVUAVURVUJVUEVUHVUAVURTZ
      VUIVUEVUOJLZVUJVUHVUSTZUYMJJWHUYGUXDVUTVVATZUYGUXDGEUFZUYDTZGJUAZHJUAZVVB
      UYFVVFFUWPUWIFESZUYEVVDHGJJVVGUXSVVCUYDFEGURUSUTWIVVFVUTVVCUWFVUOUYAUCZNO
      ZAPQZTZGJUAZVVAVVEVVLHVUOJUXTVUOUDZVVDVVKGJVVMUYDVVJVVCVVMUYCVVIAPVVMUYBV
      VHUWFNUXTVUOUYAVAVBVCVDWJWIVVKVUSGUYOJUYAUYOUDZVVCVUAVVJVURUYAUYOUWPWKVVN
      VVIVUQAPVVNVVHVUPUWFNUYAUYOVUOVFVBVCVGWIWLWLWMWNWOWPVUEVUJVUNVURVPVUAVUEV
      UMVUQAPVUEUYMVUPUWFNUWDWQZVUEUYMVUPUDJJWRZUYMUWDWSWTVBVCXAXBUYNVUMUXBAPUY
      NUYMUWQNOZVUMUXBTUWQUYMNUWOUWPXCZIULZXDXEVUMVVQUXBUWFUYMUWQNAULZVVSVVRXDX
      FXGUKXHXIXJXKXLXMXNXOXPXQUYSUXAUXCUXDUXAUYSUXCUXAUWQUWQNOZUYSUWQPLZUXCUXA
      VWAUWOUWPXRXSUWPUWOXRXSUDZUWOUWPDULZUXIXTUXAVWAVWCVPUWOUWPUWOUWPYAYBYCUXA
      UWQUWDLZUYSVWBTUWOUWPJJYDVWBVWEUYSUWIUYMNOZUYOUWIVJVKZVLOZVMZTZIUWDUAZUYS
      CUWQUWDPUWIUWQUDZVWJUYRIUWDVWLVWFUYNVWIUYQUWIUWQUYMNYEVWLVWHUYPVWLVWGUWPU
      YOVLVWLVWGUWQVJVKUWPUWIUWQVJYFUWOUWPVWDUXIYGYHVBYIVGWJCIYJZYKYLUKVWBVWAUX
      CUXBVWAAUWQPUWFUWQUWQNYEUOXGYMYNYOYPYQYRYSYTUXLDJUUAUKUXKEJUUAWLXLYNUUBUU
      CUWRUWGUXBAPBUWQUWFNUUDVCUUEUUFUUGUWFUWONOZUWIUWONOZRZUWLTZCPUAAPUAUWNDBU
      WDUWOBUDZVWQUWMACPPVWRVWPUWKUWLVWRVWNUWGVWOUWJUWOBUWFNUUDUWOBUWINUUDUUHUS
      UTVWQACPVWPUWFUWINOZUWFPLZUWIPLZRZUWLVWOVWNUWOUWINOVWSUWIUWONCULZVWDXDXEU
      WFUWOUWINVVTVWDVXCXDXFVTVXBVWSUWLVXBVWSRZUWFWGVKZUWFVJVKZUCZUWIWGVKZVWGUC
      ZUWFUWIVXDVXEVXHVXFVWGVXDUWFUWDLZVXFVXEXRXSZVXFVXHXRXSZUDZVXEVXHUDZVWTVXJ
      VXAVWSUWFUUIZUUJZVXDVXKVXEVWGXRXSZVXLVXDVXKVXEVXFXRXSVXQVXFVXEUWFVJUUKUWF
      WGUUKXTVXDVXFVWGVXEXRVXDVXFVWGUDZVWGVXFVLOZVXBVWSVXSVMZVXAVWTVWSVXTTZVXAU
      WIUWDLZVUMUYOVXFVLOZVMZTZIUWDUAZVYAVWTUWIUUIZVWTVXJVYFVWKVYFCUWFUWDPCASZV
      WJVYEIUWDVYHVWFVUMVWIVYDUWIUWFUYMNYEVYHVWHVYCVYHVWGVXFUYOVLUWIUWFVJYFVBYI
      VGWJVWMYKUULVYEVYAIUWIUWDICSZVUMVWSVYDVXTUYMUWIUWFNUUDVYIVYCVXSVYIUYOVWGV
      XFVLUYMUWIVJYFUUMYIVGUUNUUOVSWMVXDVXRVXSVXDVXFVWGVLOZVMZVXRVXSUUPZVXBVWSV
      YKVWSUWIUWFNOZVXBVYKUWFUWINVVTVXCXDXEVWTVXJVWKVYMVYKTZVXAVXOVXAVYBVWKVWKC
      PUWDVWMUUQUULVWJVYNIUWFUWDIASZVWFVYMVWIVYKUYMUWFUWINUUDVYOVWHVYJVYOUYOVXF
      VWGVLUYMUWFVJYFUUMYIVGUUNUUOWNWMVXDVXFJLZVWGJLZVYKVYLVPVWTVYPVXAVWSVWTVXJ
      VYPVXOUWFJJVQZUKUUJVXAVYQVWTVWSVXAVYBVYQVYGUWIJJVQZUKUURVYPVYQRZVYKVYLVMZ
      VMVYLVYTVYJWUAJVLUUSVYTVYJWUAVPUUTJVXFVWGVLUVAWTYIVYLUVBUVCUVDUVEUVFUVGZU
      VHUVIVXBVWSVXQVXLUDZVWTVXJVYBVWSWUCVPVXAVXOVYGVXJVYBRVWSVXGVXINOZWUCVXJVY
      BUWFVXGUWIVXINVVOVXJUWFVXGUDZVVPUWFUWDWSZWTVVOVYBUWIVXIUDZVVPUWIUWDWSZWTU
      VJVXJVXEJLZVYPRVXHJLZVYQRWUDWUCVPVYBVXJWUIVYPUWFJJWHZVYRUVKVYBWUJVYQUWIJJ
      WHVYSUVKVXEVXFVXHVWGYAUUOUVLUUOUVMUVNVXJVYPWUIVXMVXNTVYRWUKVXFVXEVXHUWIWG
      UUKUVOUVDUVPWUBUVQVXDVVOVXJWUEVVPVXPWUFUVRVXDVVOVYBWUGVVPVXAVYBVWTVWSVYGU
      URWUHUVRUVSXNWNUVTUWAUWGUWJACPUWFUWIBNYEUWBUWC $.
      $( [28-Apr-2013] $)
  $}

  ${
    $d x y $.
    $( Corrolary of ~ nqereu : the function ` /Q ` is actually a function.
       (Contributed by Mario Carneiro, 6-May-2013.) $)
    nqerf $p |- /Q : ( N. X. N. ) --> Q. $=
      ( vx vy cnpi cxp cnq cerq wf wfn crn wss df-f wfun cdm wceq df-fn wrel
      cv wbr wmo wal dffun6 cvv ceq cin df-erqNEW inss2 eqsstri xpss sstri
      df-rel mpbir wcel wreu wa nqereu visset enqer ersymb reubii sylibr weu
      df-reu eumo sylbi brel simprd inss1 ssbri jca immoi 3syl wn wex simpld
      19.23aiv con3i exmo ori syl pm2.61i ax-gen mpbir2an dmss ax-mp c1q c0
      wne 1nq ne0i dmxp mp2b sseqtri wrex reurex w3a breqi wb brinxp2 bitri
      biimpri 3exp reximdvai rexex syl6 syl5 mpd eldm ssriv eqssi rnss rnxpss
      ) CCDZEFGFXLHZFIZEJXLEFKXMFLZFMZXLNFXLOXOFPZAQZBQZFRZBSZATABFUAXQFUBUBDZJ
      FXLEDZYBFUCYCUDZYCUEUCYCUFUGZXLEUHUIFUJUKYAAXRXLULZYAYFXRXSUCRZBEUMZXSEUL
      ZYGUNZBSZYAYFXSXRUCRZBEUMYHBXRUOYGYLBEXRXSUCAUPZBUPZUQURUSUTZYHYJBVAYKYGB
      EVBYJBVCVDXTYJBXTYIYGXTYFYIXRXSXLEFYNYEVEZVFFUCXRXSFYDUCUEUCYCVGUGVHVIVJV
      KYFVLXTBVMZVLYAYQYFXTYFBXTYFYIYPVNVOVPYQYAXTBVQVRVSVTWAWBXPXLXPYCMZXLFYCJ
      ZXPYRJYEFYCWCWDWEEULEWFWGYRXLNWHEWEWIXLEWJWKWLAXLXPYFYQXRXPULYFYHYQYOYHYG
      BEWMZYFYQYGBEWNYFYTXTBEWMYQYFYGXTBEYFYIYGXTXTYFYIYGWOZXTXRXSYDRZUUAXRXSFY
      DUEWPXSUBULUUBUUAWQYNXRXSXLEUCUBWRWDWSWTXAXBXTBEXCXDXEXFBXRFYMXGUTXHXIWBX
      NYCIZEYSXNUUCJYEFYCXJWDXLEXKUIWB $.
      $( [6-May-2013] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Corrolary of ~ nqereu : closure of ` /Q ` .  (Contributed by Mario
       Carneiro, 6-May-2013.) $)
    nqercl $p |- ( A e. ( N. X. N. ) -> ( /Q ` A ) e. Q. ) $=
      ( cnpi cxp cnq cerq nqerf ffvelrni ) BBCDAEFG $.
      $( [6-May-2013] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Any member of ` ( N. X. N. ) ` relates to the representative of its
       equivalence class.  (Contributed by Mario Carneiro, 6-May-2013.) $)
    nqerrel $p |- ( A e. ( N. X. N. ) -> A ~Q ( /Q ` A ) ) $=
      ( cnpi cxp wcel cerq cfv wbr ceq wceq eqid wfn wb cnq wf nqerf ffn ax-mp
      fvex fnbrfvb mpan mpbii cin df-erqNEW inss1 eqsstri ssbri syl ) ABBCZDZAA
      EFZEGZAUJHGUIUJUJIZUKUJJEUHKZUIULUKLUHMENUMOUHMEPQUHAUJEAERSTUAEHAUJEHUHM
      CZUBHUCHUNUDUEUFUG $.
      $( [6-May-2013] $)
  $}

  ${
    $d A a $.
    $( Corrolary of ~ nqereu : the function ` /Q ` acts as the identity on
       members of ` Q. ` .  (Contributed by Mario Carneiro, 6-May-2013.) $)
    nqerid $p |- ( A e. Q. -> ( /Q ` A ) = A ) $=
      ( va cv cerq cfv wceq cnq fveq2 id eqeq12d wfun wcel wbr cnpi cxp wf
      nqerf ffun ax-mp ceq cin elpqn enqer dmenq erref2 syl wb brinxp mpancom
      mpbid df-erqNEW breqi sylibr visset funbrfv mpsyl vtoclga ) BCZDEZURFZADE
      ZAFBAGURAFZUSVAURAURADHVBIJDKZURGLZURURDMZUTNNOZGDPVCQVFGDRSVDURURTVFGOUA
      ZMZVEVDURURTMZVHVDURVFLZVIURUBZURVFTUCUDUEUFVJVDVIVHUGVKURURVFGTUHUIUJURU
      RDVGUKULUMURURDBUNUOUPUQ $.
      $( [6-May-2013] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Corrolary of ~ nqereu : if two fractions are both reduced and
       equivalent, then they are equal.  (Contributed by Mario Carneiro,
       6-May-2013.) $)
    enqeq $p |- ( ( A e. Q. /\ B e. Q. /\ A ~Q B ) -> A = B ) $=
      ( vx cnq wcel ceq wbr w3a wa cv wmo wceq 3simpa cnpi cxp wreu elpqn
      3ad2ant2 nqereu wrex reu5 simprbi 3syl 3simpb simp2 enqer dmenq erref2
      syl jca eleq1 breq1 anbi12d moi syl112anc ) ADEZBDEZABFGZHZUPUQICJZDEZUTB
      FGZIZCKZUPURIZUQBBFGZIZABLUPUQURMUSBNNOZEZVBCDPZVDUQUPVIURBQZRCBSVJVBCDTV
      DVBCDUAUBUCUPUQURUDUSUQVFUPUQURUEUQUPVFURUQVIVFVKBVHFUFUGUHUIRUJVCVEVGCAB
      DDUTALVAUPVBURUTADUKUTABFULUMUTBLVAUQVBVFUTBDUKUTBBFULUMUNUO $.
      $( [28-Apr-2013] $)
  $}

  ${
    $d p q r s x y $.
    $( The function ` /Q ` acts as a substitute for equivalence classes, and it
       satisfies the fundamental requirement for equivalence representatives:
       the representatives are equal iff the members are equivalent.
       (Contributed by Mario Carneiro, 6-May-2013.) $)
    nqereq $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) ) ->
      ( A ~Q B <-> ( /Q ` A ) = ( /Q ` B ) ) ) $=
      ( vx vy vp vq vr vs cnpi cxp wcel wa ceq wbr cerq cfv wceq w3a cnq
      nqercl 3ad2ant1 3ad2ant2 nqerrel fvex enqer wss wrel cv cop cmi co wex
      copab df-enq opabssxp eqsstri relxp relss mp2 errsym syl simp3 errtr
      syl11anc enqeq syl111anc 3expia breq1 biimparc sylan syl2an expr impbid
      ) AIIJZKZBVNKZLABMNZAOPZBOPZQZVOVPVQVTVOVPVQRZVRSKZVSSKZVRVSMNZVTVOVPWBVQ
      ATUAVPVOWCVQBTUBWAVRAMNZAVSMNZWDVOVPWEVQVOAVRMNZWEAUCZAVRMAOUDUEMVNVNJZUF
      WIUGMUGMCUHZVNKDUHZVNKLWJEUHZFUHZUIQWKGUHZHUHZUIQLWLWOUJUKWMWNUJUKQLHULGU
      LFULEULZLCDUMWICDEFGHUNWPCDVNVNUOUPVNVNUQMWIURUSZUTVAUAWAVQBVSMNZWFVOVPVQ
      VBVPVOWRVQBUCZUBABVSMUEWQVCVDVRAVSMUEWQVCVDVRVSVEVFVGVOVPVTVQVOWGVRBMNZVQ
      VPVTLWHVPVSBMNZVTWTVPWRXAWSBVSMBOUDUEWQUTVAVTWTXAVRVSBMVHVIVJAVRBMUEWQVCV
      KVLVM $.
      $( [6-May-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C y z $.  $d D y z $.
    $( Addition of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 6-May-2013.) $)
    addpipq2 $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) ) ->
      ( A +pQ B ) = <. ( ( ( 1st ` A ) .N ( 2nd ` B ) ) +N
                         ( ( 1st ` B ) .N ( 2nd ` A ) ) ) ,
                         ( ( 2nd ` A ) .N ( 2nd ` B ) ) >. ) $=
      ( vx vy vz cnpi cxp cv c1st cfv c2nd cmi co cpli cop cplpq opex wceq
      fveq2 opreq1d opreq2d opreq12d opeq12d df-plpqNEW oprabval2 ) CDEABFFGZUF
      CHZIJZDHZKJZLMZUIIJZUGKJZLMZNMZUMUJLMZOAIJZBKJZLMZBIJZAKJZLMZNMZVAURLMZOP
      UQUJLMZULVALMZNMZVAUJLMZOVCVDQUGARZUOVGUPVHVIUKVEUNVFNVIUHUQUJLUGAISTVIUM
      VAULLUGAKSZUAUBVIUMVAUJLVJTUCUIBRZVGVCVHVDVKVEUSVFVBNVKUJURUQLUIBKSZUAVKU
      LUTVALUIBISTUBVKUJURVALVLUAUCCDEUDUE $.
      $( [8-May-2013] $)

    $( Addition of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    addpipqNEW $p |- ( ( ( A e. N. /\ B e. N. ) /\ ( C e. N. /\ D e. N. ) ) ->
          ( <. A , B >. +pQ <. C , D >. ) =
            <. ( ( A .N D ) +N ( C .N B ) ) , ( B .N D ) >. ) $=
      ( cnpi wcel wa cop cplpq co c1st cfv c2nd cmi cpli cxp wceq opelxpi
      addpipq2 syl2an op1stg ad2antrr op2ndg adantl opreq12d ad2antrl adantr
      opreqan12d opeq12d eqtrd ) AEFZBEFZGZCEFZDEFZGZGZABHZCDHZIJZURKLZUSMLZNJZ
      USKLZURMLZNJZOJZVEVBNJZHZADNJZCBNJZOJZBDNJZHUMUREEPZFUSVNFUTVIQUPABEERCDE
      ERURUSSTUQVGVLVHVMUQVCVJVFVKOUQVAAVBDNUKVAAQULUPABEUAUBUPVBDQUMCDEEUCZUDU
      EUQVDCVEBNUNVDCQUMUOCDEUAUFUMVEBQUPABEEUCZUGUEUEUMUPVEBVBDNVPVOUHUIUJ $.
      $( [8-May-2013] $)

    $( Addition of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    addpqnq $p |- ( ( A e. Q. /\ B e. Q. ) ->
        ( A +Q B ) = ( /Q ` ( A +pQ B ) ) ) $=
      ( vx vy vz cnq wcel wa cop cplq cfv cplpq cerq co ccom cxp cres wceq
      df-plqNEW fveq1i a1i opelxpi fvres syl cnpi wfn cv c1st c2nd cmi cpli
      opex df-plpqNEW fnoprab2 elpqn syl2an wfun wf nqerf ffun ax-mp fvco2
      mp3an1 sylancr 3eqtrd df-opr fveq2i 3eqtr4g ) AFGZBFGZHZABIZJKZVLLKZMKZAB
      JNABLNZMKVKVMVLMLOZFFPZQZKZVLVQKZVOVMVTRVKVLJVSSTUAVKVLVRGVTWARABFFUBVLVR
      VQUCUDVKLUEUEPZWBPZUFZVLWCGZWAVORZCDEWBWBCUGZUHKDUGZUIKZUJNWHUHKWGUIKZUJN
      UKNZWJWIUJNZILWKWLULCDEUMUNVIAWBGBWBGWEVJAUOBUOABWBWBUBUPMUQZWDWEWFWBFMUR
      WMUSWBFMUTVAWCVLMLVBVCVDVEABJVFVPVNMABLVFVGVH $.
      $( [8-May-2013] $) $( [28-Aug-1995] $)

    $( Multiplication of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    mulpipq2 $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) ) ->
      ( A .pQ B ) = <. ( ( 1st ` A ) .N ( 1st ` B ) ) ,
                       ( ( 2nd ` A ) .N ( 2nd ` B ) ) >. ) $=
      ( vx vy vz cnpi cxp cv c1st cfv cmi co c2nd cop cmpq opex wceq fveq2
      opreq1d opeq12d opreq2d df-mpqNEW oprabval2 ) CDEABFFGZUDCHZIJZDHZIJZKLZU
      EMJZUGMJZKLZNAIJZBIJZKLZAMJZBMJZKLZNOUMUHKLZUPUKKLZNUOURPUEAQZUIUSULUTVAU
      FUMUHKUEAIRSVAUJUPUKKUEAMRSTUGBQZUSUOUTURVBUHUNUMKUGBIRUAVBUKUQUPKUGBMRUA
      TCDEUBUC $.
      $( [8-May-2013] $)

    $( Multiplication of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    mulpipqNEW $p |- ( ( ( A e. N. /\ B e. N. ) /\ ( C e. N. /\ D e. N. ) ) ->
          ( <. A , B >. .pQ <. C , D >. ) = <. ( A .N C ) , ( B .N D ) >. ) $=
      ( cnpi wcel wa cop cmpq co c1st cfv cmi c2nd cxp wceq opelxpi mulpipq2
      syl2an op1stg ad2antrr ad2antrl opreq12d op2ndg opreqan12d opeq12d eqtrd
      ) AEFZBEFZGZCEFZDEFZGZGZABHZCDHZIJZUOKLZUPKLZMJZUONLZUPNLZMJZHZACMJZBDMJZ
      HUJUOEEOZFUPVGFUQVDPUMABEEQCDEEQUOUPRSUNUTVEVCVFUNURAUSCMUHURAPUIUMABETUA
      UKUSCPUJULCDETUBUCUJUMVABVBDMABEEUDCDEEUDUEUFUG $.
      $( [8-May-2013] $) $( [28-Aug-1995] $)

    $( Multiplication of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    mulpqnq $p |- ( ( A e. Q. /\ B e. Q. ) ->
        ( A .Q B ) = ( /Q ` ( A .pQ B ) ) ) $=
      ( vx vy vz cnq wcel wa cop cmq cfv cmpq cerq co ccom cxp cres wceq
      df-mqNEW fveq1i a1i opelxpi fvres syl cnpi wfn cv c1st cmi c2nd opex
      df-mpqNEW fnoprab2 elpqn syl2an wfun wf nqerf ffun ax-mp fvco2 mp3an1
      sylancr 3eqtrd df-opr fveq2i 3eqtr4g ) AFGZBFGZHZABIZJKZVKLKZMKZABJNABLNZ
      MKVJVLVKMLOZFFPZQZKZVKVPKZVNVLVSRVJVKJVRSTUAVJVKVQGVSVTRABFFUBVKVQVPUCUDV
      JLUEUEPZWAPZUFZVKWBGZVTVNRZCDEWAWACUGZUHKDUGZUHKUINZWFUJKWGUJKUINZILWHWIU
      KCDEULUMVHAWAGBWAGWDVIAUNBUNABWAWAUBUOMUPZWCWDWEWAFMUQWJURWAFMUSUTWBVKMLV
      AVBVCVDABJVEVOVMMABLVEVFVG $.
      $( [8-May-2013] $) $( [28-Aug-1995] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.
    ordpipqNEW.1 $e |- A e. _V $.
    ordpipqNEW.2 $e |- B e. _V $.
    ordpipqNEW.3 $e |- C e. _V $.
    ordpipqNEW.4 $e |- D e. _V $.
    $( Ordering of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    ordpipqNEW $p |- ( <. A , B >. <pQ <. C , D >. <->
                          ( A .N D ) <N ( C .N B ) ) $=
      ( vx vy cop cltpq wbr cnpi cxp wcel wa cmi co clti cv c1st cfv c2nd opex
      wceq eleq1 anbi1d fveq2 op1st syl6eq opreq1d op2nd opreq2d breq12d
      anbi12d anbi2d df-ltpqNEW brab simpr oprex ltrelpi brel dmmulpi 0npi
      ndmoprrcl anim12i opelxpi ad2ant2rl simprl simplr syl11anc jca 3syl
      ancri impbii bitri ) ABKZCDKZLMVRNNOZPZVSVTPZQZADRSZCBRSZTMZQZWFIUAZVTPZJ
      UAZVTPZQZWHUBUCZWJUDUCZRSZWJUBUCZWHUDUCZRSZTMZQWAWKQZAWNRSZWPBRSZTMZQWGIJ
      VRVSLABUECDUEWHVRUFZWLWTWSXCXDWIWAWKWHVRVTUGUHXDWOXAWRXBTXDWMAWNRXDWMVRUB
      UCAWHVRUBUIABEUJUKULXDWQBWPRXDWQVRUDUCBWHVRUDUIABEFUMUKUNUOUPWJVSUFZWTWCX
      CWFXEWKWBWAWJVSVTUGUQXEXAWDXBWETXEWNDARXEWNVSUDUCDWJVSUDUICDGHUMUKUNXEWPC
      BRXEWPVSUBUCCWJVSUBUICDGUJUKULUOUPIJURUSWGWFWCWFUTWFWCWFWDNPZWENPZQANPZDN
      PZQZCNPZBNPZQZQZWCWDWENNTCBRVAVBVCXFXJXGXMADNRHVDVEVFCBNRFVDVEVFVGXNWAWBX
      HXLWAXIXKABNNVHVIXNXKXIWBXJXKXLVJXHXIXMVKCDNNVHVLVMVNVOVPVQ $.
      $( [8-May-2013] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Ordering of positive fractions in terms of positive integers.
       (Contributed by Mario Carneiro, 28-Apr-2013.) $)
    ordpinq $p |- ( ( A e. Q. /\ B e. Q. ) ->
        ( A <Q B <-> ( ( 1st ` A ) .N ( 2nd ` B ) ) <N
                      ( ( 1st ` B ) .N ( 2nd ` A ) ) ) ) $=
      ( cnq wcel wa cltpq wbr cltq c1st cfv c2nd cmi co clti cxp cin brinxp
      df-ltnqNEW breqi syl6bbr cop cnpi wrel wceq relxp elpqn 1st2nd sylancr
      breqan12d fvex ordpipqNEW syl6bb bitr3d ) ACDZBCDZEZABFGZABHGZAIJZBKJZLMB
      IJZAKJZLMNGZUPUQABFCCOPZGURABCCFQABHVDRSTUPUQUSVBUAZVAUTUAZFGVCUNUOAVEBVF
      FUNUBUBOZUCZAVGDAVEUDUBUBUEZAUFAVGUGUHUOVHBVGDBVFUDVIBUFBVGUGUHUIUSVBVAUT
      AIUJAKUJBIUJBKUJUKULUM $.
      $( [28-Apr-2013] $) $( [13-Feb-1996] $)
  $}

  ${
    $d A x y z $.  $d B y z $.
    $( Closure of addition on positive fractions.  (Contributed by Mario
       Carneiro, 8-May-2013.) $)
    addpqf $p |- +pQ : ( ( N. X. N. ) X. ( N. X. N. ) ) --> ( N. X. N. ) $=
      ( vx vy vz cnpi cxp cv c1st cfv c2nd cmi co cpli cop cplpq df-plpqNEW
      wcel wa xp1st xp2nd mulclpi syl2an adantl adantr syl11anc addclpi
      opelxpi foprab ) ABCDDEZUHAFZGHZBFZIHZJKZUKGHZUIIHZJKZLKZUOULJKZMZUHNABCO
      UIUHPZUKUHPZQZUQDPZURDPZUSUHPVBUMDPZUPDPZVCUTUJDPULDPZVEVAUIDDRUKDDSZUJUL
      TUAVBUNDPZUODPZVFVAVIUTUKDDRUBUTVJVAUIDDSZUCUNUOTUDUMUPUEUDUTVJVGVDVAVKVH
      UOULTUAUQURDDUFUDUG $.
      $( [8-May-2013] $) $( [29-Aug-1995] $)

    $( Closure of addition on positive fractions.  (Contributed by Mario
       Carneiro, 8-May-2013.) $)
    addclnq $p |- ( ( A e. Q. /\ B e. Q. ) -> ( A +Q B ) e. Q. ) $=
      ( cnq wcel wa cplq co cplpq cerq cfv addpqnq cnpi cxp elpqn addpqf
      foprcl syl2an nqercl syl eqeltrd ) ACDZBCDZEZABFGABHGZIJZCABKUCUDLLMZDZUE
      CDUAAUFDBUFDUGUBANBNABUFUFUFHOPQUDRST $.
      $( [8-May-2013] $) $( [29-Aug-1995] $)

    $( Closure of multiplication on positive fractions.  (Contributed by Mario
       Carneiro, 8-May-2013.) $)
    mulpqf $p |- .pQ : ( ( N. X. N. ) X. ( N. X. N. ) ) --> ( N. X. N. ) $=
      ( vx vy vz cnpi cxp cv c1st cfv cmi co c2nd cop cmpq df-mpqNEW wcel wa
      xp1st mulclpi syl2an xp2nd opelxpi syl11anc foprab ) ABCDDEZUDAFZGHZBFZGH
      ZIJZUEKHZUGKHZIJZLZUDMABCNUEUDOZUGUDOZPUIDOZULDOZUMUDOUNUFDOUHDOUPUOUEDDQ
      UGDDQUFUHRSUNUJDOUKDOUQUOUEDDTUGDDTUJUKRSUIULDDUAUBUC $.
      $( [8-May-2013] $) $( [29-Aug-1995] $)

    $( Closure of multiplication on positive fractions.  (Contributed by Mario
       Carneiro, 8-May-2013.) $)
    mulclnq $p |- ( ( A e. Q. /\ B e. Q. ) -> ( A .Q B ) e. Q. ) $=
      ( cnq wcel wa cmq co cmpq cerq cfv mulpqnq cnpi cxp elpqn mulpqf foprcl
      syl2an nqercl syl eqeltrd ) ACDZBCDZEZABFGABHGZIJZCABKUCUDLLMZDZUECDUAAUF
      DBUFDUGUBANBNABUFUFUFHOPQUDRST $.
      $( [8-May-2013] $) $( [29-Aug-1995] $)
  $}

  ${
    $d w x y z $.
    $( Domain of addition on positive fractions.  (Contributed by Mario
       Carneiro, 28-Apr-2013.) $)
    dmaddnq $p |- dom +Q = ( Q. X. Q. ) $=
      ( vx cplq cdm cerq cplpq ccom cnq cxp cres cin df-plqNEW dmeqi dmres wss
      wceq cnpi cv elpqn ssriv xpss12 mp2an wfn crn wf nqerf ffn ax-mp addpqf
      frn fnco mp3an fndm sseqtr4i df-ss mpbi 3eqtri ) BCDEFZGGHZIZCURUQCZJZURB
      USKLUQURMURUTNVAUROURPPHZVBHZUTGVBNZVDURVCNAGVBAQRSZVEGVBGVBTUAUQVCUBZUTV
      CODVBUBZEVCUBZEUCVBNZVFVBGDUDVGUEVBGDUFUGVCVBEUDZVHUHVCVBEUFUGVJVIUHVCVBE
      UIUGVBVCDEUJUKVCUQULUGUMURUTUNUOUP $.
      $( [28-Apr-2013] $) $( [24-Aug-1995] $)

    $( Domain of multiplication on positive fractions.  (Contributed by Mario
       Carneiro, 28-Apr-2013.) $)
    dmmulnq $p |- dom .Q = ( Q. X. Q. ) $=
      ( vx cmq cdm cerq cmpq ccom cnq cxp cres cin df-mqNEW dmeqi dmres wss
      wceq cnpi cv elpqn ssriv xpss12 mp2an wfn crn wf nqerf ffn ax-mp mulpqf
      frn fnco mp3an fndm sseqtr4i df-ss mpbi 3eqtri ) BCDEFZGGHZIZCURUQCZJZURB
      USKLUQURMURUTNVAUROURPPHZVBHZUTGVBNZVDURVCNAGVBAQRSZVEGVBGVBTUAUQVCUBZUTV
      CODVBUBZEVCUBZEUCVBNZVFVBGDUDVGUEVBGDUFUGVCVBEUDZVHUHVCVBEUFUGVJVIUHVCVBE
      UIUGVBVCDEUJUKVCUQULUGUMURUTUNUOUP $.
      $( [28-Apr-2013] $) $( [24-Aug-1995] $)
  $}

  ${
    $d A x $.  $d B x $.
    addcompqNEW.1 $e |- A e. _V $.
    addcompqNEW.2 $e |- B e. _V $.
    $( Addition of positive fractions is commutative.  (Contributed by Mario
       Carneiro, 28-Apr-2013.) $)
    addcompqNEW $p |- ( A +pQ B ) = ( B +pQ A ) $=
      ( cnpi cxp wcel wa cplpq co wceq c1st cfv c2nd cmi cpli cop oprex
      addcompi fvex mulcompi opeq12i addpipq2 ancoms 3eqtr4a addpqf fdmi
      ndmoprcom pm2.61i ) AEEFZGZBUJGZHZABIJZBAIJZKUMALMZBNMZOJZBLMZANMZOJZPJZU
      TUQOJZQVAURPJZUQUTOJZQZUNUOVBVDVCVEURVAUPUQORUSUTORSUTUQANTBNTUAUBABUCULU
      KUOVFKBAUCUDUEABUJIDUJUJFUJIUFUGCUHUI $.
      $( [28-Apr-2013] $) $( [30-Aug-1995] $)

    $( Addition of positive fractions is commutative.  (Contributed by Mario
       Carneiro, 28-Apr-2013.) $)
    addcomnq $p |- ( A +Q B ) = ( B +Q A ) $=
      ( cnq wcel wa cplq co wceq cplpq cerq cfv addcompqNEW fveq2i addpqnq
      ancoms 3eqtr4a dmaddnq ndmoprcom pm2.61i ) AEFZBEFZGZABHIZBAHIZJUDABKIZLM
      BAKIZLMZUEUFUGUHLABCDNOABPUCUBUFUIJBAPQRABEHDSCTUA $.
      $( [28-Apr-2013] $) $( [30-Aug-1995] $)

    $( Multiplication of positive fractions is commutative.  (Contributed by
       Mario Carneiro, 28-Apr-2013.) $)
    mulcompqNEW $p |- ( A .pQ B ) = ( B .pQ A ) $=
      ( cnpi cxp wcel wa cmpq co wceq c1st cfv cmi c2nd cop fvex mulcompi
      opeq12i mulpipq2 ancoms 3eqtr4a mulpqf fdmi ndmoprcom pm2.61i ) AEEFZGZBU
      GGZHZABIJZBAIJZKUJALMZBLMZNJZAOMZBOMZNJZPUNUMNJZUQUPNJZPZUKULUOUSURUTUMUN
      ALQBLQRUPUQAOQBOQRSABTUIUHULVAKBATUAUBABUGIDUGUGFUGIUCUDCUEUF $.
      $( [28-Apr-2013] $) $( [31-Aug-1995] $)

    $( Multiplication of positive fractions is commutative.  (Contributed by
       Mario Carneiro, 28-Apr-2013.) $)
    mulcomnq $p |- ( A .Q B ) = ( B .Q A ) $=
      ( cnq wcel wa cmq co wceq cmpq cerq cfv mulcompqNEW fveq2i mulpqnq
      ancoms 3eqtr4a dmmulnq ndmoprcom pm2.61i ) AEFZBEFZGZABHIZBAHIZJUDABKIZLM
      BAKIZLMZUEUFUGUHLABCDNOABPUCUBUFUIJBAPQRABEHDSCTUA $.
      $( [28-Apr-2013] $) $( [31-Aug-1995] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.
    $( Lemma for ~ adderpq . $)
    adderpqlem $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) /\
        C e. ( N. X. N. ) ) ->
      ( A ~Q B <-> ( A +pQ C ) ~Q ( B +pQ C ) ) ) $=
      ( vx vy vz cnpi cxp wcel w3a c1st cfv c2nd cmi co cpli cop ceq wbr wceq
      cplpq wb xp1st 3ad2ant1 xp2nd 3ad2ant3 mulclpi syl11anc addclpi 3ad2ant2
      enqbreq syl22anc addpipq2 3adant2 3adant1 breq12d enqbreq2 3adant3 oprex
      mulcanpiNEW mulcompi fvex cv visset mulasspi caopr4 eqtri opreq12i
      addcompi distrpi caoprdistrr 3eqtr4i 3eqtrri opreq1i 3eqtri opreq2i
      eqeq12i addcanpi syl5rbbr 3bitr2d 3bitr4rd ) AGGHZIZBWBIZCWBIZJZAKLZCMLZN
      OZCKLZAMLZNOZPOZWKWHNOZQZBKLZWHNOZWJBMLZNOZPOZWRWHNOZQZRSZWMXANOZWNWTNOZT
      ZACUAOZBCUAOZRSABRSZWFWMGIZWNGIZWTGIZXAGIZXCXFUBWFWIGIZWLGIZXJWFWGGIZWHGI
      ZXNWCWDXPWEAGGUCUDZWEWCXQWDCGGUEUFZWGWHUGUHWFWJGIZWKGIZXOWEWCXTWDCGGUCUFZ
      WCWDYAWEAGGUEUDZWJWKUGUHWIWLUIUHWFYAXQXKYCXSWKWHUGUHZWFWQGIZWSGIZXLWFWPGI
      ZXQYEWDWCYGWEBGGUCUJXSWPWHUGUHWFXTWRGIZYFYBWDWCYHWEBGGUEUJZWJWRUGUHZWQWSU
      IUHWFYHXQXMYIXSWRWHUGUHWMWNWTXAUKULWFXGWOXHXBRWCWEXGWOTWDACUMUNWDWEXHXBTW
      CBCUMUOUPWFXIWGWRNOZWPWKNOZTZWHWHNOZYKNOZYNYLNOZTZXFWCWDXIYMUBWEABUQURWFY
      NGIZYKGIZYQYMUBWFXQXQYRXSXSWHWHUGUHZWFXPYHYSXRYIWGWRUGUHZYNYKYLWPWKNUSZUT
      UHXFWNWSNOZYOPOZUUCYPPOZTZWFYQUUDXDUUEXEYOUUCPOWIXANOZWLXANOZPOUUDXDYOUUG
      UUCUUHPYOYKYNNOUUGYNYKWHWHNUSWGWRNUSVADEFWGWRWHWHNAKVBBMVBZCMVBZDVCZEVCZD
      VDEVDZVAZUUKUULFVCZUUMFVDZVEZUUJVFVGUUCWKWJNOZWHWRNOZNOUUHDEFWKWHWJWRNAMV
      BZUUJCKVBZUUNUUQUUIVFUURWLUUSXANWKWJUUTUVAVAWHWRUUJUUIVAVHVGVHUUCYOWNWSNU
      SZYNYKNUSVIDEFWIWLXAPNWGWHNUSWJWKNUSWRWHNUSUUNUUKUULUUOUUMUUPVJVKVLUUCWNW
      QNOZPOUVCUUCPOUUEXEUUCUVCUVBWNWQNUSVIYPUVCUUCPYPWHWHYLNOZNOZWNWPNOZWHNOZU
      VCWHWHYLUUJUUBVEUVEUVDWHNOUVGWHUVDUUJWHYLNUSVAUVDUVFWHNUVFWKWHWPNOZNOUVHW
      KNOUVDWKWHWPUUJBKVBZVEWKUVHUUTWHWPNUSVAWHWPWKUVIUUTVEVMVNVGWNWPWHUVIUUJVE
      VOVPWNWQWSWPWHNUSWJWRNUSVJVLVQWFUUCGIZYOGIZUUFYQUBWFXKYFUVJYDYJWNWSUGUHWF
      YRYSUVKYTUUAYNYKUGUHUUCYOYPYNYLNUSVRUHVSVTWA $.
      $( [8-May-2013] $)

    $( Lemma for ~ mulerpq . $)
    mulerpqlem $p |- ( ( A e. ( N. X. N. ) /\ B e. ( N. X. N. ) /\
        C e. ( N. X. N. ) ) ->
      ( A ~Q B <-> ( A .pQ C ) ~Q ( B .pQ C ) ) ) $=
      ( vx vy vz cnpi cxp wcel w3a c1st cfv cmi co c2nd cop ceq wbr wceq cmpq
      wb xp1st 3ad2ant1 3ad2ant3 mulclpi syl11anc xp2nd 3ad2ant2 enqbreq
      syl22anc mulpipq2 3adant2 3adant1 breq12d enqbreq2 3adant3 oprex
      mulcanpiNEW mulcompi fvex cv visset mulasspi caopr4 eqtri 3eqtri eqeq12i
      a1i 3bitr2d 3bitr4rd ) AGGHZIZBVKIZCVKIZJZAKLZCKLZMNZAOLZCOLZMNZPZBKLZVQM
      NZBOLZVTMNZPZQRZVRWFMNZWAWDMNZSZACTNZBCTNZQRABQRZVOVRGIZWAGIZWDGIZWFGIZWH
      WKUAVOVPGIZVQGIZWOVLVMWSVNAGGUBUCZVNVLWTVMCGGUBUDZVPVQUEUFVOVSGIZVTGIZWPV
      LVMXCVNAGGUGUCVNVLXDVMCGGUGUDZVSVTUEUFVOWCGIZWTWQVMVLXFVNBGGUBUHXBWCVQUEU
      FVOWEGIZXDWRVMVLXGVNBGGUGUHZXEWEVTUEUFVRWAWDWFUIUJVOWLWBWMWGQVLVNWLWBSVMA
      CUKULVMVNWMWGSVLBCUKUMUNVOWNVPWEMNZWCVSMNZSZVQVTMNZXIMNZXLXJMNZSZWKVLVMWN
      XKUAVNABUOUPVOXLGIZXIGIZXOXKUAVOWTXDXPXBXEVQVTUEUFVOWSXGXQXAXHVPWEUEUFXLX
      IXJWCVSMUQZURUFXOWKUAVOXMWIXNWJXMXIXLMNWIXLXIVQVTMUQZVPWEMUQUSDEFVPWEVQVT
      MAKUTBOUTCKUTZDVAZEVAZDVBEVBZUSZYAYBFVAYCFVBVCZCOUTZVDVEXNXJXLMNWDWAMNWJX
      LXJXSXRUSDEFWCVSVQVTMBKUTAOUTXTYDYEYFVDWDWAWCVQMUQVSVTMUQUSVFVGVHVIVJ $.
      $( [8-May-2013] $)
  $}

  ${
    adderpq.1 $e |- B e. _V $.
    $( Addition is compatible with the equivalence relation.  (Contributed by
       Mario Carneiro, 8-May-2013.) $)
    adderpq $p |- ( ( /Q ` A ) +Q ( /Q ` B ) ) = ( /Q ` ( A +pQ B ) ) $=
      ( cnpi cxp wcel wa cerq cfv cplq co cplpq wceq cnq nqercl addpqnq syl2an
      ceq wbr nqerrel adantr wb wi elpqn syl adderpqlem 3exp mpd imp mpbid
      adantl mpan9 fvex addcompqNEW 3brtr3g oprex enqer ertr syl11anc addpqf
      foprcl nqereq eqtr4d wn c0 0nnq cdm nqerf fdmi eleq2i notbii ndmfv
      sylbir eleq1d mtbiri con4i anim12i con3i dmaddnq ndmopr 0nelxp mtbir
      pm2.61i ) ADDEZFZBWDFZGZAHIZBHIZJKZABLKZHIZMWGWJWHWILKZHIZWLWEWHNFZWINFZW
      JWNMWFAOZBOZWHWIPQWGWKWMRSZWLWNMZWGWKWHBLKZRSZXAWMRSWSWGAWHRSZXBWEXCWFATU
      AWEWFXCXBUBZWEWHWDFZWFXDUCWEWOXEWQWHUDUEZWEXEWFXDAWHBUFUGUHUIUJWGBWHLKZWI
      WHLKZXAWMRWGBWIRSZXGXHRSZWFXIWEBTUKWEXEWFXIXJUBZXFWFWIWDFZXEXKUCWFWPXLWRW
      IUDUEZWFXLXEXKBWIWHUFUGUHULUJBWHCAHUMZUNWIWHBHUMZXNUNUOWKXAWMRABLUPWHBLUP
      WHWILUPUQURUSWGWKWDFWMWDFZWSWTUBABWDWDWDLUTVAWEXEXLXPWFXFXMWHWIWDWDWDLUTV
      AQWKWMVBUSUJVCWGVDZWJVEWLXQWOWPGZVDWJVEMXRWGWOWEWPWFWEWOWEVDZWOVENFZVFXSW
      HVENXSAHVGZFZVDWHVEMYBWEYAWDAWDNHVHVIZVJVKAHVLVMVNVOVPWFWPWFVDZWPXTVFYDWI
      VENYDBYAFZVDWIVEMYEWFYAWDBYCVJVKBHVLVMVNVOVPVQVRWHWINJXOVSVTUEXQWKYAFZVDW
      LVEMXQYFVEYAFZYGVEWDFDDWAYAWDVEYCVJWBXQWKVEYAABWDLCWDWDEWDLUTVIVTVNVOWKHV
      LUEVCWC $.
      $( [8-May-2013] $)

    $( Multiplication is compatible with the equivalence relation.
       (Contributed by Mario Carneiro, 8-May-2013.) $)
    mulerpq $p |- ( ( /Q ` A ) .Q ( /Q ` B ) ) = ( /Q ` ( A .pQ B ) ) $=
      ( cnpi cxp wcel wa cerq cfv cmq co cmpq wceq cnq nqercl mulpqnq syl2an
      ceq wbr nqerrel adantr wb wi elpqn syl mulerpqlem 3exp mpd imp mpbid
      adantl mpan9 fvex mulcompqNEW 3brtr3g oprex enqer ertr syl11anc mulpqf
      foprcl nqereq eqtr4d wn c0 0nnq cdm nqerf fdmi eleq2i notbii ndmfv
      sylbir eleq1d mtbiri con4i anim12i con3i dmmulnq ndmopr 0nelxp mtbir
      pm2.61i ) ADDEZFZBWDFZGZAHIZBHIZJKZABLKZHIZMWGWJWHWILKZHIZWLWEWHNFZWINFZW
      JWNMWFAOZBOZWHWIPQWGWKWMRSZWLWNMZWGWKWHBLKZRSZXAWMRSWSWGAWHRSZXBWEXCWFATU
      AWEWFXCXBUBZWEWHWDFZWFXDUCWEWOXEWQWHUDUEZWEXEWFXDAWHBUFUGUHUIUJWGBWHLKZWI
      WHLKZXAWMRWGBWIRSZXGXHRSZWFXIWEBTUKWEXEWFXIXJUBZXFWFWIWDFZXEXKUCWFWPXLWRW
      IUDUEZWFXLXEXKBWIWHUFUGUHULUJBWHCAHUMZUNWIWHBHUMZXNUNUOWKXAWMRABLUPWHBLUP
      WHWILUPUQURUSWGWKWDFWMWDFZWSWTUBABWDWDWDLUTVAWEXEXLXPWFXFXMWHWIWDWDWDLUTV
      AQWKWMVBUSUJVCWGVDZWJVEWLXQWOWPGZVDWJVEMXRWGWOWEWPWFWEWOWEVDZWOVENFZVFXSW
      HVENXSAHVGZFZVDWHVEMYBWEYAWDAWDNHVHVIZVJVKAHVLVMVNVOVPWFWPWFVDZWPXTVFYDWI
      VENYDBYAFZVDWIVEMYEWFYAWDBYCVJVKBHVLVMVNVOVPVQVRWHWINJXOVSVTUEXQWKYAFZVDW
      LVEMXQYFVEYAFZYGVEWDFDDWAYAWDVEYCVJWBXQWKVEYAABWDLCWDWDEWDLUTVIVTVNVOWKHV
      LUEVCWC $.
      $( [8-May-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.
    addassnq.1 $e |- B e. _V $.
    addassnq.2 $e |- C e. _V $.
    $( Addition of positive fractions is associative.  (Contributed by Mario
       Carneiro, 28-Apr-2013.) $)
    addassnq $p |- ( ( A +Q B ) +Q C ) = ( A +Q ( B +Q C ) ) $=
      ( vx vy vz cnq wcel w3a cplq co wceq cplpq cerq cfv c1st c2nd cmi cpli
      cop oprex addasspi fvex cv visset mulcompi distrpi caoprdistrr mulasspi
      opreq1i eqtri caopr32 opreq2i opreq12i 3eqtr4i opeq12i cnpi cxp elpqn
      3ad2ant1 3ad2ant2 addpipq2 syl11anc wrel relxp 3ad2ant3 1st2nd sylancr
      opreq12d xp1st syl xp2nd mulclpi addclpi addpipqNEW syl22anc eqtrd
      3eqtr4a fveq2d adderpq 3eqtr4g addpqnq 3adant3 nqerid eqcomd 3adant1
      3eqtr4d dmaddnq 0nnq ndmoprass pm2.61i ) AIJZBIJZCIJZKZABLMZCLMZABCLMZLMZ
      NWQABOMZPQZCPQZLMZAPQZBCOMZPQZLMZWSXAWQXBCOMZPQAXGOMZPQXEXIWQXJXKPWQARQZB
      SQZTMZBRQZASQZTMZUAMZCSQZTMZCRQZXPXMTMZTMZUAMZYBXSTMZUBZXLXMXSTMZTMZXOXST
      MZYAXMTMZUAMZXPTMZUAMZXPYGTMZUBZXJXKYDYMYEYNYHXQXSTMZUAMZYCUAMYHYPYCUAMZU
      AMYDYMYHYPYCXQXSTUCYAYBTUCUDXTYQYCUAXTXNXSTMZYPUAMYQFGHXNXQXSUATXLXMTUCXO
      XPTUCCSUEZFUFZGUFZFUGGUGZUHZUUAUUBHUFZUUCHUGZUIZUJYSYHYPUAXLXMXSBSUEZYTUK
      ULUMULYLYRYHUAYLYIXPTMZYJXPTMZUAMYRFGHYIYJXPUATXOXSTUCYAXMTUCASUEZUUDUUGU
      JUUIYPUUJYCUAFGHXOXSXPTBRUEYTUUKUUDUUAUUBUUEUUCUUFUKUNUUJYAXMXPTMZTMYCYAX
      MXPUUHUUKUKUULYBYATXMXPUUHUUKUHUOUMUPUMUOUQXPXMXSUUHYTUKURWQXJXRYBUBZYAXS
      UBZOMZYFWQXBUUMCUUNOWQAUSUSUTZJZBUUPJZXBUUMNWNWOUUQWPAVAVBZWOWNUURWPBVAVC
      ZABVDVEWQUUPVFZCUUPJZCUUNNUSUSVGZWPWNUVBWOCVAVHZCUUPVIVJVKWQXRUSJZYBUSJZY
      AUSJZXSUSJZUUOYFNWQXNUSJZXQUSJZUVEWQXLUSJZXMUSJZUVIWQUUQUVKUUSAUSUSVLVMZW
      QUURUVLUUTBUSUSVNVMZXLXMVOVEWQXOUSJZXPUSJZUVJWQUURUVOUUTBUSUSVLVMZWQUUQUV
      PUUSAUSUSVNVMZXOXPVOVEXNXQVPVEWQUVPUVLUVFUVRUVNXPXMVOVEWQUVBUVGUVDCUSUSVL
      VMZWQUVBUVHUVDCUSUSVNVMZXRYBYAXSVQVRVSWQXKXLXPUBZYKYGUBZOMZYOWQAUWAXGUWBO
      WQUVAUUQAUWANUVCUUSAUUPVIVJWQUURUVBXGUWBNUUTUVDBCVDVEVKWQUVKUVPYKUSJZYGUS
      JZUWCYONUVMUVRWQYIUSJZYJUSJZUWDWQUVOUVHUWFUVQUVTXOXSVOVEWQUVGUVLUWGUVSUVN
      YAXMVOVEYIYJVPVEWQUVLUVHUWEUVNUVTXMXSVOVEXLXPYKYGVQVRVSVTWAXBCEWBAXGBCOUC
      WBWCWQWRXCCXDLWNWOWRXCNWPABWDWEWPWNCXDNWOWPXDCCWFWGVHVKWQAXFWTXHLWNWOAXFN
      WPWNXFAAWFWGVBWOWPWTXHNWNBCWDWHVKWIABCILDWJEWKWLWM $.
      $( [28-Apr-2013] $) $( [2-Sep-1995] $)

    $( Multiplication of positive fractions is associative.  (Contributed by
       Mario Carneiro, 28-Apr-2013.) $)
    mulassnq $p |- ( ( A .Q B ) .Q C ) = ( A .Q ( B .Q C ) ) $=
      ( cnq wcel w3a cmq co wceq cmpq cerq cfv c1st cmi c2nd cop fvex mulasspi
      opeq12i cnpi cxp elpqn 3ad2ant1 3ad2ant2 mulpipq2 syl11anc wrel relxp
      3ad2ant3 1st2nd sylancr opreq12d xp1st syl mulclpi xp2nd mulpipqNEW
      syl22anc eqtrd 3eqtr4a fveq2d mulerpq oprex 3eqtr4g mulpqnq 3adant3
      nqerid eqcomd 3adant1 3eqtr4d dmmulnq 0nnq ndmoprass pm2.61i ) AFGZBFGZCF
      GZHZABIJZCIJZABCIJZIJZKVTABLJZMNZCMNZIJZAMNZBCLJZMNZIJZWBWDVTWECLJZMNAWJL
      JZMNWHWLVTWMWNMVTAONZBONZPJZCONZPJZAQNZBQNZPJZCQNZPJZRZWOWPWRPJZPJZWTXAXC
      PJZPJZRZWMWNWSXGXDXIWOWPWRBOSCOSTWTXAXCBQSCQSTUAVTWMWQXBRZWRXCRZLJZXEVTWE
      XKCXLLVTAUBUBUCZGZBXNGZWEXKKVQVRXOVSAUDUEZVRVQXPVSBUDUFZABUGUHVTXNUIZCXNG
      ZCXLKUBUBUJZVSVQXTVRCUDUKZCXNULUMUNVTWQUBGZXBUBGZWRUBGZXCUBGZXMXEKVTWOUBG
      ZWPUBGZYCVTXOYGXQAUBUBUOUPZVTXPYHXRBUBUBUOUPZWOWPUQUHVTWTUBGZXAUBGZYDVTXO
      YKXQAUBUBURUPZVTXPYLXRBUBUBURUPZWTXAUQUHVTXTYEYBCUBUBUOUPZVTXTYFYBCUBUBUR
      UPZWQXBWRXCUSUTVAVTWNWOWTRZXFXHRZLJZXJVTAYQWJYRLVTXSXOAYQKYAXQAXNULUMVTXP
      XTWJYRKXRYBBCUGUHUNVTYGYKXFUBGZXHUBGZYSXJKYIYMVTYHYEYTYJYOWPWRUQUHVTYLYFU
      UAYNYPXAXCUQUHWOWTXFXHUSUTVAVBVCWECEVDAWJBCLVEVDVFVTWAWFCWGIVQVRWAWFKVSAB
      VGVHVSVQCWGKVRVSWGCCVIVJUKUNVTAWIWCWKIVQVRAWIKVSVQWIAAVIVJUEVRVSWCWKKVQBC
      VGVKUNVLABCFIDVMEVNVOVP $.
      $( [8-May-2013] $) $( [1-Sep-1995] $)
  $}

  ${
    $d A b c $.  $d B b c $.  $d C c $.
    $( Lemma for distributive law: cancellation of common factor.  (Contributed
       by Mario Carneiro, 8-May-2013.) $)
    mulcanenq $p |- ( ( A e. N. /\ B e. N. /\ C e. N. ) ->
      <. ( A .N B ) , ( A .N C ) >. ~Q <. B , C >. ) $=
      ( vb vc cnpi wcel cmi co cop ceq wbr wa cv wi wceq opreq2 opeq1d opeq1
      breq12d imbi2d opeq2d opeq2 w3a visset mulcompi opreq2i mulasspi 3eqtr4i
      wb mulclpi 3adant3 3adant2 3simpc enqbreq syl21anc mpbiri 3expb expcom
      vtocl2ga impcom 3impb ) AFGZBFGZCFGZABHIZACHIZJZBCJZKLZVDVEMVCVJVCADNZHIZ
      AENZHIZJZVKVMJZKLZOVCVFVNJZBVMJZKLZOVCVJODEBCFFVKBPZVQVTVCWAVOVRVPVSKWAVL
      VFVNVKBAHQRVKBVMSTUAVMCPZVTVJVCWBVRVHVSVIKWBVNVGVFVMCAHQUBVMCBUCTUAVCVKFG
      ZVMFGZMZVQVCWCWDVQVCWCWDUDZVQVLVMHIZVNVKHIZPZAVKVMHIZHIAVMVKHIZHIWGWHWJWK
      AHVKVMDUEZEUEZUFUGAVKVMWLWMUHAVMVKWMWLUHUIWFVLFGZVNFGZWEVQWIUJVCWCWNWDAVK
      UKULVCWDWOWCAVMUKUMVCWCWDUNVLVNVKVMUOUPUQURUSUTVAVB $.
      $( [8-May-2013] $) $( [2-Sep-1995] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.
    distrnq.1 $e |- B e. _V $.
    distrnq.2 $e |- C e. _V $.
    $( Multiplication of positive fractions is distributive.  (Contributed by
       Mario Carneiro, 28-Apr-2013.) $)
    distrnq $p |- ( A .Q ( B +Q C ) ) = ( ( A .Q B ) +Q ( A .Q C ) ) $=
      ( vx vy vz cnq wcel w3a cplq co cmq wceq cerq cfv cplpq cmpq ceq wbr
      c1st cmi c2nd cpli cop fvex mulcompi opreq1i cv visset mulasspi caopr411
      eqtri opreq12i oprex distrpi 3eqtr2i caopr12 opreq2i opeq12i cnpi cxp
      elpqn 3ad2ant1 xp2nd syl xp1st 3ad2ant2 3ad2ant3 mulclpi syl11anc
      addclpi mulcanenq syl111anc syl5eqbr mulpipq2 opreq12d addpipqNEW
      syl22anc eqtrd wrel relxp 1st2nd sylancr addpipq2 mulpipqNEW 3brtr4d wb
      mulpqf foprcl addpqf nqereq mpbid eqcomd mulerpq adderpq 3eqtr4g nqerid
      addpqnq 3adant1 mulpqnq 3adant3 3adant2 3eqtr4d dmaddnq 0nnq dmmulnq
      ndmoprdistr pm2.61i ) AIJZBIJZCIJZKZABCLMZNMZABNMZACNMZLMZOXNAPQZBCRMZPQZ
      NMZABSMZPQZACSMZPQZLMZXPXSXNAYASMZPQZYDYFRMZPQZYCYHXNYLYJXNYKYITUAZYLYJOZ
      XNAUBQZBUBQZUCMZAUDQZCUDQZUCMZUCMZYOCUBQZUCMZYRBUDQZUCMZUCMZUEMZUUEYTUCMZ
      UFZYOYPYSUCMZUUBUUDUCMZUEMZUCMZYRUUDYSUCMZUCMZUFZYKYITXNUUIYRUUMUCMZYRUUO
      UCMZUFZUUPTUUGUUQUUHUURUUGYRYOUCMZUUJUCMZUUTUUKUCMZUEMUUTUULUCMUUQUUAUVAU
      UFUVBUEUUAYPYOUCMZYTUCMUVAYQUVCYTUCYOYPAUBUGZBUBUGZUHUIFGHYPYOYRYSUCUVEUV
      DAUDUGZFUJZGUJZFUKGUKZUHZUVGUVHHUJUVIHUKULZCUDUGZUMUNUUFUUBYOUCMZUUEUCMUV
      BUUCUVMUUEUCYOUUBUVDCUBUGZUHUIFGHUUBYOYRUUDUCUVNUVDUVFUVJUVKBUDUGZUMUNUOU
      UTUUJUUKYPYSUCUPUUBUUDUCUPUQYRYOUULUVDUUJUUKUEUPULURUUHYRUUDYTUCMZUCMUURY
      RUUDYTUVOYRYSUCUPULUVPUUOYRUCFGHUUDYRYSUCUVOUVFUVLUVJUVKUSUTUNVAXNYRVBJZU
      UMVBJZUUOVBJZUUSUUPTUAXNAVBVBVCZJZUVQXKXLUWAXMAVDVEZAVBVBVFVGZXNYOVBJZUUL
      VBJZUVRXNUWAUWDUWBAVBVBVHVGZXNUUJVBJZUUKVBJZUWEXNYPVBJZYSVBJZUWGXNBUVTJZU
      WIXLXKUWKXMBVDVIZBVBVBVHVGZXNCUVTJZUWJXMXKUWNXLCVDVJZCVBVBVFVGZYPYSVKVLXN
      UUBVBJZUUDVBJZUWHXNUWNUWQUWOCVBVBVHVGZXNUWKUWRUWLBVBVBVFVGZUUBUUDVKVLUUJU
      UKVMVLZYOUULVKVLXNUVQUUNVBJZUVSUWCXNUWRUWJUXBUWTUWPUUDYSVKVLZYRUUNVKVLYRU
      UMUUOVNVOVPXNYKYQUUEUFZUUCYTUFZRMZUUIXNYDUXDYFUXERXNUWAUWKYDUXDOUWBUWLABV
      QVLXNUWAUWNYFUXEOUWBUWOACVQVLVRXNYQVBJZUUEVBJZUUCVBJZYTVBJZUXFUUIOXNUWDUW
      IUXGUWFUWMYOYPVKVLXNUVQUWRUXHUWCUWTYRUUDVKVLXNUWDUWQUXIUWFUWSYOUUBVKVLXNU
      VQUWJUXJUWCUWPYRYSVKVLYQUUEUUCYTVSVTWAXNYIYOYRUFZUULUUNUFZSMZUUPXNAUXKYAU
      XLSXNUVTWBUWAAUXKOVBVBWCUWBAUVTWDWEXNUWKUWNYAUXLOUWLUWOBCWFVLVRXNUWDUVQUW
      EUXBUXMUUPOUWFUWCUXAUXCYOYRUULUUNWGVTWAWHXNYKUVTJZYIUVTJZYMYNWIXNYDUVTJZY
      FUVTJZUXNXNUWAUWKUXPUWBUWLABUVTUVTUVTSWJWKVLXNUWAUWNUXQUWBUWOACUVTUVTUVTS
      WJWKVLYDYFUVTUVTUVTRWLWKVLXNUWAYAUVTJZUXOUWBXNUWKUWNUXRUWLUWOBCUVTUVTUVTR
      WLWKVLAYAUVTUVTUVTSWJWKVLYKYIWMVLWNWOAYABCRUPWPYDYFACSUPWQWRXNAXTXOYBNXKX
      LAXTOXMXKXTAAWSWOVEXLXMXOYBOXKBCWTXAVRXNXQYEXRYGLXKXLXQYEOXMABXBXCXKXMXRY
      GOXLACXBXDVRXEABCILNDXFEXGXHXIXJ $.
      $( [28-Apr-2013] $) $( [2-Sep-1995] $)
  $}

  ${
    1nqenq.1 $e |- A e. _V $.
    $( The equivalence class of ratio 1.  (Contributed by Mario Carneiro,
       28-Apr-2013.) $)
    1nqenq $p |- ( A e. N. -> 1Q ~Q <. A , A >. ) $=
      ( cnpi wcel cop c1q ceq wbr c1o cmi co mulidpi opeq12d 1pi mulcanenq
      mp3an23 df-1nq syl6breqr eqbrtrrd opex cvv eqeltri enqer ersym syl ) ACDZ
      AAEZFGHFUGGHUFAIJKZUHEZUGFGUFUHAUHAALZUJMUFUIIIEZFGUFICDZULUIUKGHNNAIIOPQ
      RSUGFGAATFUKUAQIITUBUCUDUE $.
      $( [28-Apr-2013] $) $( [4-Mar-1996] $)
  $}

  ${
    $d x y A $.
    $( Multiplication identity element for positive fractions.  (Contributed by
       Mario Carneiro, 28-Apr-2013.) $)
    mulidnq $p |- ( A e. Q. -> ( A .Q 1Q ) = A ) $=
      ( cnq wcel c1q cmq co cmpq cerq cfv wceq 1nq mulpqnq mpan2 c1st c2nd cop
      c1o cmi cnpi cxp wrel relxp elpqn 1st2nd sylancr df-1nq a1i opreq12d
      xp1st syl xp2nd 1pi mulpipqNEW syl22anc mulidpi opeq12d 3eqtrd eqtr4d
      fveq2d nqerid ) ABCZADEFZADGFZHIZAHIAVADBCVBVDJKADLMVAVCAHVAVCANIZAOIZPZA
      VAVCVGQQPZGFZVEQRFZVFQRFZPZVGVAAVGDVHGVASSTZUAAVMCZAVGJSSUBAUCZAVMUDUEZDV
      HJVAUFUGUHVAVESCZVFSCZQSCZVSVIVLJVAVNVQVOASSUIZUJVAVNVRVOASSUKZUJVSVAULUG
      ZWBVEVFQQUMUNVAVNVLVGJVOVNVJVEVKVFVNVQVJVEJVTVEUOUJVNVRVKVFJWAVFUOUJUPUJU
      QVPURUSAUTUQ $.
      $( [28-Apr-2013] $) $( [3-Mar-1996] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d r s t x y $.
    recmulnq.1 $e |- B e. _V $.
    $( Relationship between reciprocal and multiplication on positive
       fractions.  (Contributed by Mario Carneiro, 8-May-2013.) $)
    recmulnq $p |- ( A e. Q. ->
      ( ( *Q ` A ) = B <-> ( A .Q B ) = 1Q ) ) $=
      ( vx vy vr vs vt cv cmq co c1q wceq cnq crq opreq1 eqeq1d opreq2 wcel
      wex weu c2nd cfv c1st cop cerq cmpq cmi nqerid cnpi cxp wrel relxp elpqn
      1st2nd sylancr fveq2d eqtr3d opreq1d opex mulerpq syl6eq xp1st syl xp2nd
      mulpipqNEW syl22anc fvex mulcompi opeq2i 1nq ax-mp ceq wbr mulclpi
      syl11anc oprex 1nqenq wb opelxpi nqereq mpbid syl5reqr 3eqtrd cla4ev wmo
      eu5 visset dmmulnq 0nnq mulcomnq mulassnq mulidnq caoprmo mpbiran2
      sylibr copab wa df-rqNEW eleq1 mpbiri ndmoprrcl simpl 3syl pm4.71ri
      opabbii eqtri fvopab3 ) DIZEIZJKZLMZAXJJKZLMABJKZLMDEABNOCXIAMXKXMLXIAXJJ
      PQXJBMXMXNLXJBAJRQXINSZXLETZXLEUAZXOXIXIUBUCZXIUDUCZUEZUFUCZJKZLMZXPXOYBX
      SXRUEZXTUGKZUFUCZXSXRUHKZYGUEZUFUCZLXOYBYDUFUCZYAJKYFXOXIYJYAJXOXIUFUCXIY
      JXIUIXOXIYDUFXOUJUJUKZULXIYKSZXIYDMUJUJUMXIUNZXIYKUOUPUQURUSYDXTXRXSUTVAV
      BXOYEYHUFXOYEYGXRXSUHKZUEZYHXOXSUJSZXRUJSZYQYPYEYOMXOYLYPYMXIUJUJVCVDZXOY
      LYQYMXIUJUJVEVDZYSYRXSXRXRXSVFVGYNYGYGXRXSXIUBVHXIUDVHVIVJVBUQXOLLUFUCZYI
      LNSZYTLMVKLUIVLXOLYHVMVNZYTYIMZXOYGUJSZUUBXOYPYQUUDYRYSXSXRVOVPZYGXSXRUHV
      QVRVDXOLYKSZYHYKSZUUBUUCVSUUAUUFVKLUNVLXOUUDUUDUUGUUEUUEYGYGUJUJVTVPLYHWA
      UPWBWCWDXLYCEYAXTUFVHXJYAMXKYBLXJYAXIJRQWEVDXQXPXLEWFXLEWGFGHEXILNJDWHVKW
      IWJFIZGIZFWHGWHZWKUUHUUIHIUUJHWHWLUUHWMWNWOWPOXLDEWQXOXLWRZDEWQDEWSXLUUKD
      EXLXOXLXKNSZXOXJNSZWRXOXLUULUUAVKXKLNWTXAXIXJNJEWHWIWJXBXOUUMXCXDXEXFXGXH
      $.
      $( [8-May-2013] $) $( [6-Mar-1996] $)
  $}

  $( A positive fraction times its reciprocal is 1.  (Contributed by Mario
     Carneiro, 8-May-2013.) $)
  recidnq $p |- ( A e. Q. -> ( A .Q ( *Q ` A ) ) = 1Q ) $=
    ( cnq wcel crq cfv wceq cmq co c1q eqid fvex recmulnq mpbii ) ABCADEZNFANGH
    IFNJANADKLM $.
    $( [8-May-2013] $) $( [6-Mar-1996] $)

  $( Closure law for positive fraction reciprocal.  (Contributed by Mario
     Carneiro, 8-May-2013.) $)
  recclnq $p |- ( A e. Q. -> ( *Q ` A ) e. Q. ) $=
    ( cnq wcel crq cfv cmq co wa c1q recidnq 1nq syl6eqel fvex dmmulnq 0nnq
    ndmoprrcl syl simprd ) ABCZSADEZBCZSATFGZBCSUAHSUBIBAJKLATBFADMNOPQR $.
    $( [8-May-2013] $) $( [6-Mar-1996] $)

  ${
    $d A x $.
    $( Reciprocal of reciprocal of positive fraction.  (Contributed by Mario
       Carneiro, 29-Apr-2013.) $)
    recrecnq $p |- ( A e. Q. -> ( *Q ` ( *Q ` A ) ) = A ) $=
      ( vx cv crq cfv wceq cnq fveq2 fveq2d id eqeq12d wcel cmq co c1q fvex
      visset mulcomnq recidnq syl5eq wb recclnq recmulnq syl mpbird vtoclga )
      BCZDEZDEZUGFZADEZDEZAFBAGUGAFZUIULUGAUMUHUKDUGADHIUMJKUGGLZUJUHUGMNZOFZUN
      UOUGUHMNOUHUGUGDPBQZRUGSTUNUHGLUJUPUAUGUBUHUGUQUCUDUEUF $.
      $( [29-Apr-2013] $) $( [26-Apr-1996] $)
  $}

  ${
    $d x y $.
    $( Domain of reciprocal on positive fractions.  (Contributed by Mario
       Carneiro, 29-Apr-2013.) $)
    dmrecnq $p |- dom *Q = Q. $=
      ( vx vy crq cdm cv cmq co c1q wceq copab wex cab cnq df-rqNEW dmeqi
      dmopab wcel wb abeq1 1nq eleq1 mpbiri visset dmmulnq 0nnq ndmoprrcl
      simpld syl 19.23aiv cfv recidnq fvex opreq2 eqeq1d cla4ev impbii mpgbir
      3eqtri ) CDAEZBEZFGZHIZABJZDVBBKZALZMCVCABNOVBABPVEMIVDUSMQZRAVDAMSVDVFVB
      VFBVBVAMQZVFVBVGHMQTVAHMUAUBVGVFUTMQUSUTMFBUCUDUEUFUGUHUIVFUSUSCUJZFGZHIZ
      VDUSUKVBVJBVHUSCULUTVHIVAVIHUTVHUSFUMUNUOUHUPUQUR $.
      $( [29-Apr-2013] $) $( [6-Mar-1996] $)
  $}

  ${
    $d r s t x y z $.
    $( 'Less than' is a strict ordering on positive fractions.  (Contributed by
       Mario Carneiro, 4-May-2013.) $)
    ltsonq $p |- <Q Or Q. $=
      ( vx vy vz vr vs vt cnq cltq cv wcel w3a wbr weq wo wn wb wa wi c1st cfv
      c2nd cmi co clti wceq cnpi cxp elpqn adantr xp1st syl adantl xp2nd
      mulclpi syl11anc wor ltsopi sotric mpan ordpinq fveq2 eqcomd opreq12d
      ceq enqbreq2 syl2an enqeq 3expia sylbird impbid2 ancoms orbi12d notbid
      3bitr4d 3adant3 3ad2ant3 oprex ltmpi 3syl bitrd 3adant1 3ad2ant1 anbi12d
      fvex visset mulcompi mulasspi caopr13 breq12i ltrelpi sotri syl5eqbrr
      sylan2b syl6bi 3adant2 3ad2ant2 sylibrd jca so ) ABCGHAIZGJZBIZGJZCIZGJZK
      ZWTXBHLZABMZXBWTHLZNZOZPZXGXBXDHLZQZWTXDHLZRXAXCXLXEXAXCQZWTSTZXBUATZUBUC
      ZXBSTZWTUATZUBUCZUDLZXSYBUEZYBXSUDLZNZOZXGXKXPXSUFJZYBUFJZYCYGPZXPXQUFJZX
      RUFJZYHXPWTUFUFUGZJZYKXAYNXCWTUHZUIZWTUFUFUJUKXPXBYMJZYLXCYQXAXBUHZULZXBU
      FUFUMZUKXQXRUNUOXPXTUFJZYAUFJZYIXPYQUUAYSXBUFUFUJUKXPYNUUBYPWTUFUFUMZUKXT
      YAUNUOUFUDUPYHYIQYJUQUFXSYBUDURUSUOWTXBUTZXPXJYFXPXHYDXIYEXPXHYDXHXQXTXRY
      AUBWTXBSVAXHYAXRWTXBUAVAVBVCXPYDWTXBVDLZXHXAYNYQUUEYDPXCYOYRWTXBVEVFXAXCU
      UEXHWTXBVGVHVIVJXCXAXIYEPXBWTUTVKVLVMVNVOXFXNXRXQXDUATZUBUCZUBUCZXRXDSTZY
      AUBUCZUBUCZUDLZXOXFXNUUFXSUBUCZUUFYBUBUCZUDLZYAXTUUFUBUCZUBUCZYAUUIXRUBUC
      ZUBUCZUDLZQUULXFXGUUOXMUUTXFXGYCUUOXAXCXGYCPXEUUDVOXFXDYMJZUUFUFJYCUUOPXE
      XAUVAXCXDUHVPXDUFUFUMXSYBUUFXQXRUBVQXTYAUBVQVRVSVTXFXMUUPUURUDLZUUTXCXEXM
      UVBPXAXBXDUTWAXFYNUUBUVBUUTPXAXCYNXEYOWBUUCUUPUURYAXTUUFUBVQUUIXRUBVQVRVS
      VTWCUUTUUOUUNUUKUDLZUULUUQUUNUUSUUKUDDEFYAXTUUFUBWTUAWDZXBSWDXDUAWDZDIZEI
      ZDWEEWEZWFZUVFUVGFIUVHFWEWGZWHDEFYAUUIXRUBUVDXDSWDXBUAWDZUVIUVJWHWIUUOUVC
      QUUHUUMUUKUDDEFUUFXQXRUBUVEWTSWDUVKUVIUVJWHUUMUUNUUKUDUFUQWJWKWLWMWNXFXOU
      UGUUJUDLZUULXAXEXOUVLPXCWTXDUTWOXFYQYLUVLUULPXCXAYQXEYRWPYTUUGUUJXRXQUUFU
      BVQUUIYAUBVQVRVSVTWQWRWS $.
      $( [4-May-2013] $) $( [19-Feb-1996] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( Compatibility of ordering on equivalent fractions.  (Contributed by
       Mario Carneiro, 9-May-2013.) $)
    lterpq $p |- ( A <pQ B <-> ( /Q ` A ) <Q ( /Q ` B ) ) $=
      ( vz vx vy cltpq wbr cnpi cxp wcel wa cerq cfv cltq cv c1st c2nd cmi co
      clti copab df-ltpqNEW opabssxp eqsstri wn wi wor ltsopi xp1st xp2nd
      mulclpi syl11anc sonr sylancr adantr imnan mpbi visset weq eleq1 anbi1d
      fveq2 opreq1d opreq2d breq12d anbi12d anbi2d brab mtbir irrexi cnq fvex
      ltrelnq brel elpqn nqerf fdmi 0nelxp ndmfvrcl anim12i syl2an syl cop
      wrel wceq relxp 1st2nd mpan breqan12d ordpipqNEW syl6bb wb nqercl 3syl
      oprex ltmpi mulcompi a1i ceq nqerrel enqbreq2 mpdan mpbid eqcomd
      opreqan12d mulasspi caopr411 eqtri 3eqtr4g bitrd ordpinq bitr2d 3bitrd
      pm5.21nii ) ABFGZAHHIZJZBXPJZKZALMZBLMZNGZCABFXPFDOZXPJZEOZXPJZKZYCPMZYEQ
      MZRSZYEPMZYCQMZRSZTGZKZDEUAXPXPIDEUBZYNDEXPXPUCUDCOZYQFGYQXPJZYRKZYQPMZYQ
      QMZRSZUUBTGZKZYSUUCUEZUFUUDUEYRUUEYRYRHTUGUUBHJZUUEUHYRYTHJUUAHJUUFYQHHUI
      YQHHUJYTUUAUKULHUUBTUMUNUOYSUUCUPUQYOYRYFKZYTYIRSZYKUUARSZTGZKUUDDEYQYQFC
      URZUUKDCUSZYGUUGYNUUJUULYDYRYFYCYQXPUTVAUULYJUUHYMUUITUULYHYTYIRYCYQPVBVC
      UULYLUUAYKRYCYQQVBVDVEVFECUSZUUGYSUUJUUCUUMYFYRYRYEYQXPUTVGUUMUUHUUBUUIUU
      BTUUMYIUUAYTRYEYQQVBVDUUMYKYTUUARYEYQPVBVCVEVFYPVHVIVJYBXTVKJZYAVKJZKXSXT
      YAVKVKNBLVLVMVNUUNXTXPJZYAXPJZXSUUOXTVOZYAVOZUUPXQUUQXRAXPLXPVKLVPVQZHHVR
      ZVSBXPLUUTUVAVSVTWAWBXSXOAPMZBQMZRSZBPMZAQMZRSZTGZUVDXTPMZYAQMZRSZRSZUVDY
      APMZXTQMZRSZRSZTGZYBXSXOUVBUVFWCZUVEUVCWCZFGUVHXQXRAUVRBUVSFXPWDZXQAUVRWE
      HHWFZAXPWGWHUVTXRBUVSWEUWABXPWGWHWIUVBUVFUVEUVCAPVLZAQVLZBPVLZBQVLZWJWKXS
      UVHUVKUVDRSZUVKUVGRSZTGZUVQXSUVKHJZUVHUWHWLXQUVIHJZUVJHJZUWIXRXQUUNUUPUWJ
      AWMZUURXTHHUIWNXRUUOUUQUWKBWMZUUSYAHHUJWNUVIUVJUKWAUVDUVGUVKUVBUVCRWOZUVE
      UVFRWOZWPWBXSUWFUVLUWGUVPTUWFUVLWEXSUVKUVDUVIUVJRWOZUWNWQWRXSUVIUVFRSZUVE
      UVJRSZRSZUVBUVNRSZUVMUVCRSZRSZUWGUVPXQXRUWQUWTUWRUXARXQUWTUWQXQAXTWSGZUWT
      UWQWEZAWTXQUUPUXCUXDWLXQUUNUUPUWLUURWBAXTXAXBXCXDXRBYAWSGZUWRUXAWEZBWTXRU
      UQUXEUXFWLXRUUOUUQUWMUUSWBBYAXAXBXCXEUWGUVGUVKRSUWSUVKUVGUWPUWOWQDECUVEUV
      FUVIUVJRUWDUWCXTPVLYCYEDUREURZWQZYCYEYQUXGUUKXFZYAQVLXGXHUVPUVOUVDRSUXBUV
      DUVOUWNUVMUVNRWOZWQDECUVMUVNUVBUVCRYAPVLXTQVLUWBUXHUXIUWEXGXHXIVEXJXSYBUV
      KUVOTGZUVQXQUUNUUOYBUXKWLXRUWLUWMXTYAXKWAXSUVDHJZUXKUVQWLXQUVBHJUVCHJUXLX
      RAHHUIBHHUJUVBUVCUKWAUVKUVOUVDUWPUXJWPWBXLXMXN $.
      $( [9-May-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.
    ltanq.1 $e |- A e. _V $.
    ltanq.2 $e |- B e. _V $.
    $( Ordering property of addition for positive fractions.  Proposition
       9-2.6(ii) of [Gleason] p. 120.  (Contributed by Mario Carneiro,
       10-May-2013.) $)
    ltanq $p |- ( C e. Q. ->
      ( A <Q B <-> ( C +Q A ) <Q ( C +Q B ) ) ) $=
      ( vx vy vz cltq cnq cplq dmaddnq ltrelnq 0nnq wcel w3a c1st cfv c2nd cmi
      co clti wbr cplpq cltpq cpli cop wb oprex ordpipqNEW a1i cnpi cxp wceq
      elpqn 3ad2ant3 3ad2ant1 addpipq2 syl11anc 3ad2ant2 breq12d xp2nd syl
      mulclpi ltmpi xp1st ltapi mulcompi fvex cv visset mulasspi caopr411
      eqtri opreq2i distrpi 3eqtr2i opreq12i breq12i 3bitrd 3bitr4rd ordpinq
      3adant3 cerq addpqnq ancoms 3adant2 3adant1 lterpq syl6bbr 3bitr4d
      ndmord ) ABCIJKELDMNAJOZBJOZCJOZPZAQRZBSRZTUAZBQRZASRZTUAZUBUCZCAUDUAZCBU
      DUAZUEUCZABIUCZCAKUAZCBKUAZIUCZWPCQRZXATUAZWQCSRZTUAZUFUAZXMXATUAZUGZXKWR
      TUAZWTXMTUAZUFUAZXMWRTUAZUGZUEUCZXOYATUAZXTXPTUAZUBUCZXFXCYCYFUHWPXOXPXTY
      AXLXNUFUIZXMXATUIZXRXSUFUIZXMWRTUIZUJUKWPXDXQXEYBUEWPCULULUMZOZAYKOZXDXQU
      NWOWMYLWNCUOUPZWMWNYMWOAUOUQZCAURUSWPYLBYKOZXEYBUNYNWNWMYPWOBUOUTZCBURUSV
      AWPXCXMXMTUAZWSTUAZYRXBTUAZUBUCZYAXLTUAZYSUFUAZUUBYTUFUAZUBUCZYFWPYRULOZX
      CUUAUHWPXMULOZUUGUUFWPYLUUGYNCULULVBVCZUUHXMXMVDUSWSXBYRWQWRTUIZWTXATUIZV
      EVCWPUUBULOZUUAUUEUHWPYAULOZXLULOZUUKWPUUGWRULOZUULUUHWPYPUUNYQBULULVBVCX
      MWRVDUSWPXKULOZXAULOZUUMWPYLUUOYNCULULVFVCWPYMUUPYOAULULVBVCXKXAVDUSYAXLV
      DUSYSYTUUBYRWSTUIYRXBTUIVGVCUUEYFUHWPUUCYDUUDYEUBUUCUUBYAXNTUAZUFUAYAXOTU
      AYDYSUUQUUBUFYSWSYRTUAUUQYRWSXMXMTUIZUUIVHFGHWQWRXMXMTAQVIBSVIZCSVIZFVJZG
      VJZFVKGVKZVHZUVAUVBHVJUVCHVKVLZUUTVMVNVOYAXLXNXKXATUIZWQXMTUIVPYAXOYJYGVH
      VQUUDXPXRTUAZXPXSTUAZUFUAXPXTTUAYEUUBUVGYTUVHUFUUBXLYATUAUVGYAXLYJUVFVHFG
      HXKXAXMWRTCQVIASVIZUUTUVDUVEUUSVMVNYTXBYRTUAUVHYRXBUURUUJVHFGHWTXAXMXMTBQ
      VIUVIUUTUVDUVEUUTVMVNVRXPXRXSXKWRTUIWTXMTUIVPXPXTYHYIVHVQVSUKVTWAWMWNXGXC
      UHWOABWBWCWPXJXDWDRZXEWDRZIUCXFWPXHUVJXIUVKIWMWOXHUVJUNZWNWOWMUVLCAWEWFWG
      WNWOXIUVKUNZWMWOWNUVMCBWEWFWHVAXDXEWIWJWKWL $.
      $( [10-May-2013] $) $( [6-Mar-1996] $)

    $( Ordering property of multiplication for positive fractions.  Proposition
       9-2.6(iii) of [Gleason] p. 120.  (Contributed by Mario Carneiro,
       10-May-2013.) $)
    ltmnq $p |- ( C e. Q. ->
      ( A <Q B <-> ( C .Q A ) <Q ( C .Q B ) ) ) $=
      ( vx vy vz cltq cnq cmq dmmulnq ltrelnq 0nnq wcel w3a c1st cfv c2nd cmi
      co clti wbr cmpq cltpq cop wb oprex ordpipqNEW a1i cnpi cxp wceq elpqn
      3ad2ant3 3ad2ant1 mulpipq2 syl11anc 3ad2ant2 breq12d xp1st syl xp2nd
      mulclpi ltmpi fvex cv visset mulcompi mulasspi caopr4 breq12i syl6bb
      3bitr4rd ordpinq 3adant3 cerq mulpqnq ancoms 3adant2 3adant1 lterpq
      syl6bbr 3bitr4d ndmord ) ABCIJKELDMNAJOZBJOZCJOZPZAQRZBSRZTUAZBQRZASRZTUA
      ZUBUCZCAUDUAZCBUDUAZUEUCZABIUCZCAKUAZCBKUAZIUCZWICQRZWJTUAZCSRZWNTUAZUFZX
      DWMTUAZXFWKTUAZUFZUEUCZXEXJTUAZXIXGTUAZUBUCZWSWPXLXOUGWIXEXGXIXJXDWJTUHXF
      WNTUHXDWMTUHXFWKTUHUIUJWIWQXHWRXKUEWICUKUKULZOZAXPOZWQXHUMWHWFXQWGCUNUOZW
      FWGXRWHAUNUPCAUQURWIXQBXPOZWRXKUMXSWGWFXTWHBUNUSCBUQURUTWIWPXDXFTUAZWLTUA
      ZYAWOTUAZUBUCZXOWIYAUKOZWPYDUGWIXDUKOZXFUKOZYEWIXQYFXSCUKUKVAVBWIXQYGXSCU
      KUKVCVBXDXFVDURWLWOYAWJWKTUHWMWNTUHVEVBYBXMYCXNUBFGHXDXFWJWKTCQVFZCSVFZAQ
      VFFVGZGVGZFVHGVHZVIZYJYKHVGYLHVHVJZBSVFVKFGHXDXFWMWNTYHYIBQVFYMYNASVFVKVL
      VMVNWFWGWTWPUGWHABVOVPWIXCWQVQRZWRVQRZIUCWSWIXAYOXBYPIWFWHXAYOUMZWGWHWFYQ
      CAVRVSVTWGWHXBYPUMZWFWHWGYRCBVRVSWAUTWQWRWBWCWDWE $.
      $( [10-May-2013] $) $( [6-Mar-1996] $)
  $}

  $( One is less than two (one plus one).  (Contributed by Mario Carneiro,
     10-May-2013.) $)
  1lt2nq $p |- 1Q <Q ( 1Q +Q 1Q ) $=
    ( c1q cerq cfv cplpq co cplq cltq cltpq wbr c1o cop cpli cmi clti 1lt2pi
    cnpi wcel wceq 1pi mulidpi ax-mp addclpi mp2an 3brtr4i elisseti oprex
    ordpipqNEW mpbir df-1nq opreq12i addpipqNEW mp4an opeq12i 3eqtri lterpq
    mpbi cnq 1nq nqerid eqcomi addpqnq ) ABCZAADEZBCZAAAFEZGAVCHIVBVDGIJJKZJJLE
    ZJKZAVCHVFVHHIJJMEZVGJMEZNIJVGVIVJNOJPQZVIJRSJTUAZVGPQZVJVGRVKVKVMSSJJUBUCV
    GTUAUDJJVGJJPSUEZVNJJLUFVNUGUHUIVCVFVFDEZVIVILEZVIKZVHAVFAVFDUIUIUJVKVKVKVK
    VOVQRSSSSJJJJUKULVPVGVIJVIJVIJLVLVLUJVLUMUNUDAVCUOUPVBAAUQQZVBARURAUSUAUTVR
    VRVEVDRURURAAVAUCUD $.
    $( [10-May-2013] $) $( [13-Mar-1996] $)

  ${
    $d A x y $.  $d B y $.  $d r s t x y $.
    $( The sum of two fractions is greater than one of them.  (Contributed by
       Mario Carneiro, 10-May-2013.) $)
    ltaddnq $p |- ( ( A e. Q. /\ B e. Q. ) -> A <Q ( A +Q B ) ) $=
      ( vx vy vr vs vt cv cplq co cltq wbr cnq wceq id opreq1 breq12d opreq2
      breq2d wcel wa c1q cmq 1lt2nq 1nq elisseti oprex ltmnq mpbii mulidnq
      distrnq opreq12d syl5eq 3brtr3d visset ltanq syl5ib imp addcomnq
      addassnq caopr12 3brtr3g wb adantl mpbird vtocl2ga ) CHZVGDHZIJZKLZAAVHIJ
      ZKLAABIJZKLCDABMMVGANZVGAVIVKKVMOVGAVHIPQVHBNVKVLAKVHBAIRSVGMTZVHMTZUAZVJ
      VHVGIJZVHVIIJZKLZVPVIVGVHVHIJZIJZVQVRKVNVOVIWAKLZVOVHVTKLVNWBVOVHUBUCJZVH
      UBUBIJZUCJZVHVTKVOUBWDKLWCWEKLUDUBWDVHUBMUEUFZUBUBIUGUHUIVHUJZVOWEWCWCIJV
      TVHUBUBWFWFUKVOWCVHWCVHIWGWGULUMUNVHVTVGDUOZVHVHIUGUPUQURVGVHCUOZWHUSEFGV
      GVHVHIWIWHWHEHZFHZEUOFUOZUSWJWKGHWLGUOUTVAVBVOVJVSVCVNVGVIVHWIVGVHIUGUPVD
      VEVF $.
      $( [10-May-2013] $) $( [14-Mar-1996] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( Ordering on positive fractions in terms of existence of sum.  Definition
       in Proposition 9-2.6 of [Gleason] p. 119.  (Contributed by Mario
       Carneiro, 10-May-2013.) $)
    ltexnq $p |- ( B e. Q. -> ( A <Q B <-> E. x ( A +Q x ) = B ) ) $=
      ( vy vz cnq wcel cltq wbr cv cplq co wceq wex wa ltrelnq brelg c1st cfv
      c2nd cmi clti ordpinq cnpi cpli wb cxp elpqn adantr xp1st syl adantl
      xp2nd mulclpi syl11anc ltexpi cop cerq cplpq wrel relxp ad2antrr 1st2nd
      sylancr opreq1d simpr addpipqNEW syl22anc eqtrd opreq2 oprex visset
      distrpi fvex mulcompi mulasspi caopr12 opreq12i eqtr2i opreq2i eqtri
      3eqtr4g eqcomi a1i opeq12d eqeq2d syl5ibcom fveq2 opex adderpq nqerid
      syl5eqr ceq mulcanenq syl111anc ad2antlr breqtrrd opelxpi nqereq mpbid
      eqeq12d syl5ib syld eqeq1d cla4ev syl6 expimpd 19.23adv sylbid syli
      eleq1 biimparc dmaddnq 0nnq ndmoprrcl ltaddnq 3syl breqtrd ex impbid ) CF
      GZBCHIZBAJZKLZCMZANZYBYABFGZYAOZYFBCFFHFPQYHYBBRSZCTSZUALZCRSZBTSZUALZUBI
      ZYFBCUCYHYODJZUDGZYKYPUELZYNMZOZDNZYFYHYKUDGZYNUDGZYOUUAUFYHYIUDGZYJUDGZU
      UBYHBUDUDUGZGZUUDYGUUGYABUHZUIZBUDUDUJUKZYHCUUFGZUUEYAUUKYGCUHZULZCUDUDUM
      UKZYIYJUNUOYHYLUDGZYMUDGZUUCYHUUKUUOUUMCUDUDUJUKZYHUUGUUPUUIBUDUDUMUKZYLY
      MUNUODYKYNUPUOYHYTYFDYHYQYSYFYHYQOZYSBYPYMYJUALZUQZURSZKLZCMZYFUUSYSBUVAU
      SLZYMYMUALZYLUALZUVFYJUALZUQZMZUVDUUSUVEYIUUTUALZYPYMUALZUELZYMUUTUALZUQZ
      MYSUVJUUSUVEYIYMUQZUVAUSLZUVOUUSBUVPUVAUSUUSUUFUTZUUGBUVPMUDUDVAZYGUUGYAY
      QUUHVBBUUFVCVDVEUUSUUDUUPYQUUTUDGZUVQUVOMYHUUDYQUUJUIYHUUPYQUURUIYHYQVFYH
      UVTYQYHUUPUUEUVTUURUUNYMYJUNUOUIYIYMYPUUTVGVHVIYSUVOUVIUVEYSUVMUVGUVNUVHY
      SYMYRUALZYMYNUALZUVMUVGYRYNYMUAVJUWAYMYKUALZYMYPUALZUELUVMYMYKYPYIYJUAVKD
      VLZVMUWCUVKUWDUVLUEADEYMYIYJUABTVNZBRVNCTVNZYCYPAVLZUWEVOYCYPEJUWEEVLVPVQ
      YMYPUWFUWEVOVRVSUVGYMYMYLUALZUALUWBYMYMYLUWFCRVNZVPUWIYNYMUAYMYLUWFUWJVOV
      TWAWBUVNUVHMYSUVHUVNYMYMYJUWFUWGVPWCWDWEWFWGUVJUVEURSZUVIURSZMUUSUVDUVEUV
      IURWHUUSUWKUVCUWLCUUSUWKBURSZUVBKLUVCBUVAYPUUTWIWJUUSUWMBUVBKYGUWMBMYAYQB
      WKVBVEWLUUSUWLCURSZCUUSUVICWMIZUWLUWNMZUUSUVIYLYJUQZCWMUUSUVFUDGZUUOUUEUV
      IUWQWMIYHUWRYQYHUUPUUPUWRUURUURYMYMUNUOUIZYHUUOYQUUQUIZYHUUEYQUUNUIZUVFYL
      YJWNWOUUSUVRUUKCUWQMUVSYAUUKYGYQUULWPZCUUFVCVDWQUUSUVIUUFGZUUKUWOUWPUFUUS
      UVGUDGZUVHUDGZUXCUUSUWRUUOUXDUWSUWTUVFYLUNUOUUSUWRUUEUXEUWSUXAUVFYJUNUOUV
      GUVHUDUDWRUOUXBUVICWSUOWTYAUWNCMYGYQCWKWPVIXAXBXCYEUVDAUVBUVAURVNYCUVBMYD
      UVCCYCUVBBKVJXDXEXFXGXHXIXIXJYAYEYBAYAYEYBYAYEOZBYDCHUXFYDFGZYGYCFGOBYDHI
      YEUXGYAYDCFXKXLBYCFKUWHXMXNXOBYCXPXQYAYEVFXRXSXHXT $.
      $( [10-May-2013] $) $( [24-Apr-1996] $)
  $}

  ${
    $d A x $.
    $( One-half of any positive fraction exists.  Lemma for Proposition
       9-2.6(i) of [Gleason] p. 120.  (Contributed by Mario Carneiro,
       10-May-2013.) $)
    halfnq $p |- ( A e. Q. -> E. x ( x +Q x ) = A ) $=
      ( cnq wcel c1q cplq co crq cfv cmq wceq cv wex fvex distrnq 1nq addclnq
      mp2an recidnq ax-mp opreq12i eqtri opreq1i opreq2i oprex mulassnq
      mulcomnq eqtr3i recclnq syl11anc mulidnq mp2b 3eqtr3i syl5eq opreq12
      anidms eqeq1d cla4ev syl ) BCDZBEEFGZHIZJGZVCFGZBKZALZVFFGZBKZAMUTVDBEJGZ
      BBVBVBFGZJGVDVIBVBVBVAHNZVKOVJEBJVAVJJGZVBJGZVAVBJGZVJEVLVAVBJVLVNVNFGVAV
      AVBVBVKVKOVNEVNEFVACDZVNEKECDZVPVOPPEEQRZVASTZVRUAUBUCVJVNJGZVJEJGZVMVJVN
      EVJJVRUDVJVAJGZVBJGVSVMVJVAVBEEFUEZVKUFWAVLVBJVJVAVBVBFUEWBUGUCUHVOVJCDZV
      TVJKVQVOVBCDZWDWCVAUIZWEVBVBQUJVJUKULUMVRUMUDUHBUKUNVHVEAVCBVBJUEVFVCKZVG
      VDBWFVGVDKVFVCVFVCFUOUPUQURUS $.
      $( [10-May-2013] $) $( [16-Mar-1996] $)
  $}

  ${
    $d x A $.
    $( The is no smallest positive fraction.  (Contributed by Mario Carneiro,
       10-May-2013.) $)
    nsmallnq $p |- ( A e. Q. -> E. x x <Q A ) $=
      ( cnq wcel cv cplq co wceq wex cltq wbr halfnq eleq1a wa visset dmaddnq
      0nnq ndmoprrcl ltaddnq syl syl6 breq2 mpbidi eximdv mpd ) BCDZAEZUGFGZBHZ
      AIUGBJKZAIABLUFUIUJAUIUGUHJKZUJUFUFUIUHCDZUKBCUHMULUGCDZUMNUKUGUGCFAOPQRU
      GUGSTUAUHBUGJUBUCUDUE $.
      $( [10-May-2013] $) $( [26-Apr-1996] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( There exists a number between any two positive fractions.  Proposition
       9-2.6(i) of [Gleason] p. 120.  (Contributed by Mario Carneiro,
       10-May-2013.) $)
    ltbtwnnq $p |- ( A <Q B <-> E. x ( A <Q x /\ x <Q B ) ) $=
      ( vy vz cltq wbr cv wa wex cnq wcel ltrelnq ltsonq soirri irrexi simprd
      cplq co wceq ltexnq eleq1 biimparc visset dmaddnq 0nnq ndmoprrcl syl
      nsmallnq simpld brel ltaddnq syl2an ltanq biimpa sylan simplr breqtrd
      oprex breq2 breq1 anbi12d cla4ev syl11anc ex 19.23adv syl5 mpd sylbid
      mpcom sotri 19.23aiv impbii ) BCFGZBAHZFGZVOCFGZIZAJZCKLZVNVSVNBKLZVTABCF
      KMVOFKNMOPQVTVNBDHZRSZCTZDJVSDBCUAVTWDVSDVTWDVSVTWDIZWBKLZVSWEWAWFWEWCKLZ
      WAWFIWDWGVTWCCKUBUCBWBKRDUDZUEUFUGUHZQWFEHZWBFGZEJWEVSEWBUIWEWKVSEWEWKVSW
      EWKIZBBWJRSZFGZWMCFGZVSWEWAWJKLZWNWKWEWAWFWIUJZWKWPWFWJWBKKFWHMUKUJBWJULU
      MWLWMWCCFWEWAWKWMWCFGZWQWAWKWRWJWBBEUDWHUNUOUPVTWDWKUQURVRWNWOIAWMBWJRUSV
      OWMTVPWNVQWOVOWMBFUTVOWMCFVAVBVCVDVEVFVGVHVEVFVIVJVRVNABVOCFKNMVKVLVM $.
      $( [10-May-2013] $) $( [17-Mar-1996] $)
  $}

  ${
    $d A x y $.  $d B y $.
    $( Ordering property of reciprocal for positive fractions.  Proposition
       9-2.6(iv) of [Gleason] p. 120.  (Contributed by Mario Carneiro,
       10-May-2013.) $)
    ltrnq $p |- ( A <Q B <-> ( *Q ` B ) <Q ( *Q ` A ) ) $=
      ( vx vy cltq wbr cnq wcel wa crq cfv cvv wn ltsonq ltrelnq soirri brprc
      mtbiri con4i brelg mpcom fvex brel dmrecnq 0nnq ndmfvrcl anim12i ancoms
      syl cv wb wceq breq1 fveq2 breq2d bibi12d breq2 breq1d cmq co recclnq
      mulclnq syl2an visset ltmnq oprex mulcomnq mulassnq 3eqtr2i c1q recidnq
      opreq2d mulidnq sylan9eq syl5eq opreq2i eqtri sylan9eqr breq12d bitrd
      vtocl2ga pm5.21nii ) ABEFZAGHZBGHZIZBJKZAJKZEFZBLHZWCWFWJWCWJMWCAAEFAEGNO
      PABEQRSABGGELOTUAWIWGGHZWHGHZIWFWGWHGGEAJUBOUCWLWKWFWLWDWKWEAGJUDUEUFBGJU
      DUEUFUGUHUICUJZDUJZEFZWNJKZWMJKZEFZUKAWNEFZWPWHEFZUKWCWIUKCDABGGWMAULZWOW
      SWRWTWMAWNEUMXAWQWHWPEWMAJUNUOUPWNBULZWSWCWTWIWNBAEUQXBWPWGWHEWNBJUNURUPW
      MGHZWNGHZIZWOWQWPUSUTZWMUSUTZXFWNUSUTZEFZWRXEXFGHZWOXIUKXCWQGHZWPGHZXJXDW
      MVAZWNVAZWQWPVBVCWMWNXFCVDZDVDZVEUIXEXGWPXHWQEXEXGWPWMWQUSUTZUSUTZWPXGWMX
      FUSUTXQWPUSUTXRXFWMWQWPUSVFXOVGWMWQWPWMJUBWNJUBZVHXQWPWMWQUSVFXSVGVIXCXDX
      RWPVJUSUTZWPXCXQVJWPUSWMVKVLXDXLXTWPULXNWPVMUIVNVOXEXHWQWNWPUSUTZUSUTZWQX
      HWQWPWNUSUTZUSUTYBWQWPWNXSXPVHYCYAWQUSWPWNXSXPVGVPVQXDXCYBWQVJUSUTZWQXDYA
      VJWQUSWNVKVLXCXKYDWQULXMWQVMUIVRVOVSVTWAWB $.
      $( [10-May-2013] $) $( [9-Mar-1996] $)
  $}

  ${
    $d A x y $.  $d B y $.
    $( For any fraction, there is an integer that is greater than it.  This is
       also known as the "archimedean property".  (Contributed by Mario
       Carneiro, 10-May-2013.) $)
    archnq $p |- ( A e. Q. -> E. x e. N. A <Q <. x , 1o >. ) $=
      ( cnq wcel c1st cfv c1o cpli co cnpi cop cltq wbr cv wrex cxp elpqn
      xp1st syl 1pi addclpi sylancl c2nd cmi clti wn xp2nd mulclpi syl11anc wa
      wceq wex eqid elisseti eleq1 opreq2 eqeq1d anbi12d cla4ev mp2an ltexpi
      mpbiri nlt1pi wb mpan2 fvex ltmpi 3syl mulidpi breq2d bitrd mtbii ltsopi
      ltrelpi sotri3 syl111anc pinq ordpinq mpdan oprex op2nd opreq2i syl5eq
      op1st opreq1i a1i breq12d mpbird opeq1 rcla4ev ) BCDZBEFZGHIZJDZBWMGKZLMZ
      BANZGKZLMZAJOWKWLJDZGJDZWNWKBJJPDZWTBQZBJJRZSZTWLGUAZUBZWKWPWLWMBUCFZUDIZ
      UEMZWKXIJDZWLWMUEMZXIWMUEMZUFXJWKWNXHJDZXKXGWKXBXNXCBJJUGSWMXHUHUIWKWTWNX
      LXEXGWTWNUJXLWQJDZWLWQHIZWMUKZUJZAULZXAWMWMUKZXSTWMUMXRXAXTUJAGGJTUNZWQGU
      KZXOXAXQXTWQGJUOYBXPWMWMWQGWLHUPUQURUSUTAWLWMVAVBUIWKXHGUEMZXMXHVCWKYCXIW
      MGUDIZUEMZXMWKWTWNYCYEVDXEWTXAWNTXFVEZXHGWMBUCVFYAVGVHWKYDWMXIUEWKWNYDWMU
      KXGWMVISVJVKVLWLWMXIUEJVMVNVOVPWKWPWLWOUCFZUDIZWOEFZXHUDIZUEMZXJWKWOCDZWP
      YKVDWKWTWNYLXEYFWMVQVHBWOVRVSWKYHWLYJXIUEWKYHWLGUDIZWLYGGWLUDWMGWLGHVTZYA
      WAWBWKXBWTYMWLUKXCXDWLVIVHWCYJXIUKWKYIWMXHUDWMGYNWDWEWFWGVKWHWSWPAWMJWQWM
      UKWRWOBLWQWMGWIVJWJUI $.
      $( [10-May-2013] $)
  $}

  ${
    $d x y z A $.
    $( Membership in positive reals.  (Contributed by Mario Carneiro,
       11-May-2013.) $)
    elnpi $p |- ( A e. P. <-> ( ( A e. _V /\ (/) C. A /\ A C. Q. ) /\
       A. x e. A ( A. y ( y <Q x -> y e. A ) /\ E. y e. A x <Q y ) ) ) $=
      ( vz cnp wcel cvv c0 wpss cnq w3a cv cltq wbr wi wal wrex wa wral
      elisset simpl1 wel wceq psseq2 psseq1 anbi12d eleq2 imbi2d albidv rexeq
      raleqbi1dv df-np elab2g id 3expib 3simpc impbid1 anbi1d bitrd pm5.21nii
      ) CEFZCGFZVBHCIZCJIZKZBLZALZMNZVFCFZOZBPZVGVFMNZBCQZRZACSZRZCETVBVCVDVOUA
      VBVAVCVDRZVORZVPHDLZIZVSJIZRZVHBDUBZOZBPZVLBVSQZRZAVSSZRVRDCEGVSCUCZWBVQW
      HVOWIVTVCWAVDVSCHUDVSCJUEUFWGVNAVSCWIWEVKWFVMWIWDVJBWIWCVIVHVSCVFUGUHUIVL
      BVSCUJUFUKUFDABULUMVBVQVEVOVBVQVEVBVCVDVEVEUNUOVBVCVDUPUQURUSUT $.
      $( [11-May-2013] $)
  $}

  ${
    $d x y A $.
    $( A positive real is not empty.  (Revised by Mario Carneiro,
       11-May-2013.) $)
    prn0NEW $p |- ( A e. P. -> A =/= (/) ) $=
      ( vy vx cnp wcel c0 wpss wne cvv cnq w3a cv cltq wbr wi wal wrex wa wral
      elnpi simpl2 sylbi 0pss sylib ) ADEZFAGZAFHUEAIEZUFAJGZKBLZCLZMNUIAEOBPUJ
      UIMNBAQRCASZRUFCBATUGUFUHUKUAUBAUCUD $.
      $( [11-May-2013] $) $( [15-May-1996] $)

    $( A positive real is a subset of the positive fractions.  (Revised by
       Mario Carneiro, 11-May-2013.) $)
    prpssnqNEW $p |- ( A e. P. -> A C. Q. ) $=
      ( vy vx cnp wcel cvv c0 wpss cnq w3a cv cltq wbr wi wal wrex wa wral
      elnpi simpl3 sylbi ) ADEAFEZGAHZAIHZJBKZCKZLMUEAENBOUFUELMBAPQCARZQUDCBAS
      UBUCUDUGTUA $.
      $( [11-May-2013] $) $( [29-Feb-1996] $)
  $}

  $( A positive real is a set of positive fractions.  (Revised by Mario
     Carneiro, 11-May-2013.) $)
  elprnq $p |- ( ( A e. P. /\ B e. A ) -> B e. Q. ) $=
    ( cnp wcel cnq prpssnqNEW pssssd sseld imp ) ACDZBADBEDJAEBJAEAFGHI $.
    $( [11-May-2013] $) $( [13-Mar-1996] $)

  ${
    $d x y A $.  $d x y B $.  $d y C $.
    $( A positive real is closed downwards under the positive fractions.
       Definition 9-3.1 (ii) of [Gleason] p. 121.  (Revised by Mario Carneiro,
       11-May-2013.) $)
    prcdnq $p |- ( ( A e. P. /\ B e. A ) -> ( C <Q B -> C e. A ) ) $=
      ( vx vy cnp wcel wa cltq wbr wi cvv cnq cxp wss wrel ltrelnq relxp relss
      mp2 brrelexi cv wceq eleq1 anbi2d breq2 anbi12d imbi1d breq1 imbi12d wal
      wrex c0 wpss w3a wral elnpi simprbi r19.21bi simpld 19.21bi imp vtocl2g
      sylan2 adantll pm2.43i ex ) AFGZBAGZHZCBIJZCAGZVJVKHZVLVIVKVMVLKZVHVKVICL
      GVNCBIIMMNZOVOPIPQMMRIVOSTUAVHDUBZAGZHZEUBZVPIJZHZVSAGZKVJVSBIJZHZWBKVNDE
      BCALVPBUCZWAWDWBWEVRVJVTWCWEVQVIVHVPBAUDUEVPBVSIUFUGUHVSCUCZWDVMWBVLWFWCV
      KVJVSCBIUIUEVSCAUDUJVRVTWBVRVTWBKZEVRWGEUKZVPVSIJEAULZVHWHWIHZDAVHALGUMAU
      NAMUNUOWJDAUPDEAUQURUSUTVAVBVCVDVEVFVG $.
      $( [11-May-2013] $) $( [25-Feb-1996] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A positive real has no largest member.  Definition 9-3.1(iii) of
       [Gleason] p. 121.  (Revised by Mario Carneiro, 11-May-2013.) $)
    prnmaxNEW $p |- ( ( A e. P. /\ B e. A ) -> E. x e. A B <Q x ) $=
      ( vy cnp wcel cv cltq wbr wrex wa wi wceq eleq1 anbi2d breq1 rexbidv
      imbi12d wal cvv c0 wpss cnq w3a wral elnpi simprbi r19.21bi simprd
      vtoclg anabsi7 ) BEFZCBFZCAGZHIZABJZULDGZBFZKZUQUNHIZABJZLULUMKZUPLDCBUQC
      MZUSVBVAUPVCURUMULUQCBNOVCUTUOABUQCUNHPQRUSUNUQHIUNBFLASZVAULVDVAKZDBULBT
      FUABUBBUCUBUDVEDBUEDABUFUGUHUIUJUK $.
      $( [11-May-2013] $) $( [9-Mar-1996] $)
  $}

  ${
    $d A a $.  $d a x y z $.
    $( A simplifying observation, and an indication of why any attempt to
       develop a theory of the real numbers without the Axiom of Infinity is
       doomed to failure: since every member of ` P. ` is an infinite set, the
       negation of Infinity implies that ` P. ` , and hence ` RR ` , is empty.
       (Note that this proof, which used the fact that Dedekind cuts have no
       maximum, could just as well have used that they have no minimum, since
       they are downward-closed by ~ prcdnq and ~ nsmallpq ).  (Contributed by
       Mario Carneiro, 11-May-2013.) $)
    npomex $p |- ( A e. P. -> om e. _V ) $=
      ( va vx vy vz com cvv wcel cnp cv wceq biidd cfn wn cltq wbr wrex wral
      prnmaxNEW r19.21aiva wa wor c0 wne cnq wss prpssnqNEW pssssd ltsonq soss
      ee10 adantr simpr prn0NEW fimax2g syl111anc ralnex rexbii rexnal bitri
      sylib ex mt2d cen cpw crab copab eqid fineqvlem syl vtoclga ) FGHZVLBAIBJ
      ZAKVLLVMIHZVMMHZNVLVNVOCJZDJZOPZDVMQZCVMRZVNVSCVMDVMVPSTVNVOVTNZVNVOUAZVR
      NDVMRZCVMQZWAWBVMOUBZVOVMUCUDZWDVNWEVOVNVMUEUFUEOUBWEVNVMUEVMUGUHUIVMUEOU
      JUKULVNVOUMVNWFVOVMUNULCDVMOUOUPWDVSNZCVMQWAWCWGCVMVRDVMUQURVSCVMUSUTVAVB
      VCDECVPFHVQEJVPVDPEVMVEVFKUACDVGZBWHVHVIVJVK $.
      $( [11-May-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A positive real has no largest member.  Addition version.  (Revised by
       Mario Carneiro, 11-May-2013.) $)
    prnmaddNEW $p |- ( ( A e. P. /\ B e. A ) -> E. x ( B +Q x ) e. A ) $=
      ( vy cnp wcel wa cv cltq wbr wrex cplq co wex prnmaxNEW wceq cnq visset
      ltrelnq brel simprd ltexnq biimpcd mpd eleq1a eximdv syl5 r19.23aiv syl
      ) BEFCBFGCDHZIJZDBKCAHLMZBFZANZDBCOUKUNDBUKULUJPZANZUJBFZUNUKUJQFZUPUKCQF
      URCUJQQIDRSTUAURUKUPACUJUBUCUDUQUOUMAUJBULUEUFUGUHUI $.
      $( [11-May-2013] $) $( [7-Apr-1996] $)
  $}

  ${
    $d A b w x y z $.  $d B b x $.  $d b v w x y z $.
    prlem934NEW.1 $e |- B e. _V $.
    $( Lemma 9-3.4 of [Gleason] p. 122.  (Contributed by Mario Carneiro,
       11-May-2013.) $)
    prlem934NEW $p |- ( A e. P. -> E. x e. A -. ( x +Q B ) e. A ) $=
      ( vb vw vy vz vv cnp wcel cv cplq co wral wn wrex cnq c0 wne wi prn0NEW
      r19.2z ex syl prpssnqNEW pssssd sseld dmaddnq 0nnq ndmoprrcl simprd
      syl6com r19.23aivw com12 wceq opreq2 eleq1d ralbidv notbid imbi2d wa wss
      wpss dfpss2 sylib adantr w3a 3ad2ant1 wex simpl1 n0 crq cfv cmq c1o cop
      cltq wbr cnpi simprl simpl2 recclnq mulclnq archnq sylan2 syl11anc
      simpll2 simplrl simprr wb oprex opex ltmnq visset fvex mulcomnq mulassnq
      caopr12 breq12i syl6bb c1q recidnq opreq2d mulidnq sylan9eq breq1d bitrd
      biimpa syl21anc pinq sylan simpll1 simplrr elprnq ltaddnq addcomnq
      syl6breq ltsonq ltrelnq sotri simpll3 cpli opeq1 df-1nq syl6eqr opreq1d
      weq 1nq elisseti syl5eq opreq1 rcla4cva biimpar syl2an 3impb 3simpa
      addassnq distrnq caoprdistrr a1i cplpq cerq addpqnq sylancl cmi opreq2i
      1pi addpipqNEW mpanr12 mpan2 mulidpi ax-mp opreq12d opeq12d eqtrd fveq2d
      addclpi nqerid 3syl 3eqtrd adantl 3eqtr3rd syl5ib exp3a expimpd syl5 a2d
      indpi imp syl13anc prcdnq mpd exp32 r19.23adv expr 19.23adv ssrdv eqssd
      3expia mtod expcom vtoclga 3syld pm2.01d rexnal sylibr ) BJKZALZCMNZBKZAB
      OZPZUWKPABQUWHUWLUWHUWLUWKABQZCRKZUWMUWHBSTZUWLUWNUABUBZUWPUWLUWNUWKABUCU
      DUEUWNUWHUWOUWKUWHUWOUAABUWHUWKUWJRKZUWOUWHBRUWJUWHBRBUFZUGZUHUWRUWIRKUWO
      UWICRMDUIUJUKULUMUNUOUWOUWHUWMUWHUWIELZMNZBKZABOZPZUAUWHUWMUAECRUXACUPZUX
      EUWMUWHUXFUXDUWLUXFUXCUWKABUXFUXBUWJBUXACUWIMUQURUSUTVAUWHUXARKZUXEUWHUXG
      VBUXDBRUPZUWHUXHPZUXGUWHBRVCZUXIUWHBRVDUXJUXIVBUWSBRVEVFULVGUWHUXGUXDUXHU
      WHUXGUXDVHZBRUWHUXGUXJUXDUWTVIUXKFRBUXKFLZRKZUXLBKZUXKUXMVBZGLZBKZGVJZUXN
      UXOUWHUXRUWHUXGUXDUXMVKUWHUWPUXRUWQGBVLVFUEUXOUXQUXNGUXKUXMUXQUXNUXKUXMUX
      QVBZVBZUXLUXAVMVNZVONZHLZVPVQZVRVSZHVTQZUXNUXTUXMUXGUYFUXKUXMUXQWAUWHUXGU
      XDUXSWBUXGUXMUYARKZUYFUXAWCUXMUYGVBUYBRKUYFUXLUYAWDHUYBWEUEWFWGUXTUYEUXNH
      VTUXTUYCVTKZUYEUXNUXTUYHUYEVBZVBZUXLUXPUYDUXAVONZMNZVRVSZUXNUYJUXLUYKVRVS
      ZUYKUYLVRVSZUYMUYJUXGUXMUYEUYNUWHUXGUXDUXSUYIWHZUXKUXMUXQUYIWIUXTUYHUYEWJ
      UXGUXMVBZUYEUYNUYQUYEUXLUXAUYAVONZVONZUYKVRVSZUYNUXGUYEUYTWKUXMUXGUYEUXAU
      YBVONZUXAUYDVONZVRVSUYTUYBUYDUXAUXLUYAVOWLUYCVPWMZWNVUAUYSVUBUYKVRIAGUXAU
      XLUYAVOEWOZFWOUXAVMWPILZUWIIWOAWOZWQZVUEUWIUXPVUFGWOZWRWSUXAUYDVUDVUCWQWT
      XAVGUYQUYSUXLUYKVRUXGUXMUYSUXLXBVONUXLUXGUYRXBUXLVOUXAXCXDUXLXEXFXGXHXIXJ
      UYJUYKRKZUXPRKZUYOUYJUYHUXGVUIUXTUYHUYEWAZUYPUYHUYDRKZUXGVUIUYCXKZUYDUXAW
      DXLWGUYJUWHUXQVUJUWHUXGUXDUXSUYIXMZUXKUXMUXQUYIXNZBUXPXOWGVUIVUJVBUYKUYKU
      XPMNUYLVRUYKUXPXPUYKUXPUYDUXAVOWLZVUHXQXRWGUXLUYKUYLVRRXSXTYAWGUYJUWHUYLB
      KZUYMUXNUAVUNUYJUYHUXGUXDUXQVUQVUKUYPUWHUXGUXDUXSUYIYBVUOUYHUXGUXDUXQVHZV
      UQVURUXPUXLVPVQZUXAVONZMNZBKZUAVURUXPXBUXAVONZMNZBKZUAVURVUQUAZVURUXPUYCV
      PYCNZVPVQZUXAVONZMNZBKZUAVVFFHUYCUXLVPUPZVVBVVEVURVVLVVAVVDBVVLVUTVVCUXPM
      VVLVUSXBUXAVOVVLVUSVPVPVQZXBUXLVPVPYDYEYFYGXDURVAFHYHZVVBVUQVURVVNVVAUYLB
      VVNVUTUYKUXPMVVNVUSUYDUXAVOUXLUYCVPYDYGXDURVAZUXLVVGUPZVVBVVKVURVVPVVAVVJ
      BVVPVUTVVIUXPMVVPVUSVVHUXAVOUXLVVGVPYDYGXDURVAVVOUXGUXDUXQVVEUXGVVCUXAUPZ
      UXPUXAMNZBKZVVEUXDUXQVBUXGVVCUXAXBVONUXAXBUXAXBRYIYJZVUDWQUXAXEYKZUXCVVSA
      UXPBAGYHUXBVVRBUWIUXPUXAMYLURYMVVQVVEVVSVVQVVDVVRBVVCUXAUXPMUQURYNYOYPUYH
      VURVUQVVKVURUXGUXDVBUYHVUQVVKUAZUXGUXDUXQYQUYHUXGUXDVWBUYHUXGVBZUXDVUQVVK
      UXDVUQVBUYLUXAMNZBKZVWCVVKUXCVWEAUYLBUWIUYLUPUXBVWDBUWIUYLUXAMYLURYMVWCVW
      DVVJBVWCVWDUXPUYKUXAMNZMNVVJUXPUYKUXAVUPVUDYRVWCVWFVVIUXPMVWCUYDXBMNZUXAV
      ONZUYKVVCMNZVVIVWFVWHVWIUPVWCIAGUYDXBUXAMVOVUCVVTVUDVUGVUEUWIUXPVUFVUHYSY
      TUUAVWCVWGVVHUXAVOUYHVWGVVHUPUXGUYHVWGUYDXBUUBNZUUCVNZVVHUUCVNZVVHUYHVULX
      BRKVWGVWKUPVUMYIUYDXBUUDUUEUYHVWJVVHUUCUYHVWJUYCVPUUFNZVPVPUUFNZYCNZVWNVQ
      ZVVHUYHVWJUYDVVMUUBNZVWPXBVVMUYDUUBYEUUGUYHVPVTKZVWQVWPUPZUUHUYHVWRVBVWRV
      WRVWSUUHUUHUYCVPVPVPUUIUUJUUKYKUYHVWOVVGVWNVPUYHVWMUYCVWNVPYCUYCUULVWNVPU
      PZUYHVWRVWTUUHVPUULUUMUUAZUUNVXAUUOUUPUUQUYHVVGVTKZVVHRKVWLVVHUPUYHVWRVXB
      UUHUYCVPUURUUKVVGXKVVHUUSUUTUVAVGYGVWCVVCUXAUYKMUXGVVQUYHVWAUVBXDUVCXDYKU
      RUVDUVEUVFUVGUVHUVIUVJUVKBUYLUXLUVLWGUVMUVNUVOUVMUVPUVQUVMUDUVRUVSUVTUWAU
      WBUWCUOUWDUWEUWKABUWFUWG $.
      $( [11-May-2013] $) $( [25-Mar-1996] $)
  $}

  $(
  @{
    @d A a @. @d a x y z @.
    @( The rationals are countable. @)
    nqenom @p |- Q. ~~ om @=
      ? @.
  @}
  $)

  ${
    $d k m x y $.  $d A m n x y $.  $d B m n x y $.
    $( Define the sum of a series with an index set of integers ` A ` . ` k `
       is normally a free variable in ` B ` , i.e. ` B ` can be thought of as
       ` B ( k ) ` .  The definition is meaningful when ` A ` is a
       lower-bounded set of integers, since this is the most general type of
       set we can add over without the input of, say, a partial order to govern
       the order of operations.  Examples: ` sum_ k e. ( 2 ... 4 ) ` ` k `
       means 2 + 3 + 4 = 9, and ` sum_ k e. NN ` ` ( 1 / ( 2 ^ k ) ) ` means
       1/2 + 1/4 + 1/8 + ... = 1.  Note:  The restriction to ` ZZ ` force the
       class abstractions to be sets. $)
    df-sumNEW $a |- sum_ k e. A B = U. { x | E. m e. ZZ ( A C_ ( ZZ>= ` m ) /\
      ( <. m , + >. seq
        ( { <. k , y >. | y = if ( k e. A , B , 0 ) } |` ZZ ) ) ~~> x ) } $.
  $}

  ${
    $d A m x y $.  $d B m x y $.  $d k m x y $.
    $( A sum is a set. $)
    sumexNEW $p |- sum_ k e. A B e. _V $=
      ( vm vy vx csu cv cuz cfv wss caddc cop wcel cc0 cif wceq copab cz cres
      cseqz co cli wbr wa wrex cab cuni cvv df-sumNEW zex cc abid2 ax-cnex
      eqeltri visset climcl mpan adantl ss2abi ssexi abrexex2 uniex ) ABCGADHZI
      JKZVDLMEHCHANBOPQCERSTUAUBZFHZUCUDZUEZDSUFFUGZUHUIFEABCDUJVJVIDFSUKVIFUGV
      GULNZFUGZVLULUIFULUMUNUOVIVKFVHVKVEVGUINVHVKFUPVGUIVFUQURUSUTVAVBVCUO $.
      $( [12-May-2013] $) $( [11-Dec-2005] $)
  $}

  ${
    $d A k m x y $.  $d B k m x y $.  $d C m x y $.
    $( Equality theorem for a sum. $)
    sumeq1NEW $p |- ( A = B -> sum_ k e. A C = sum_ k e. B C ) $=
      ( vm vy vx wceq cv cuz cfv wss caddc cop wcel cc0 cif copab cz cres
      cseqz co cli wbr wa wrex cab cuni csu sseq1 eleq2 ifbid eqeq2d opabbidv
      reseq1 syl opreq2d breq1d anbi12d rexbidv abbidv unieqd df-sumNEW
      3eqtr4g ) ABHZAEIZJKZLZVFMNZFIZDIZAOZCPQZHZDFRZSTZUAUBZGIZUCUDZUEZESUFZGU
      GZUHBVGLZVIVJVKBOZCPQZHZDFRZSTZUAUBZVRUCUDZUEZESUFZGUGZUHACDUIBCDUIVEWBWM
      VEWAWLGVEVTWKESVEVHWCVSWJABVGUJVEVQWIVRUCVEVPWHVIUAVEVOWGHVPWHHVEVNWFDFVE
      VMWEVJVEVLWDCPABVKUKULUMUNVOWGSUOUPUQURUSUTVAVBGFACDEVCGFBCDEVCVD $.
      $( [11-Dec-2005] $)
  $}

  ${
    $d A m x y z $.  $d B m x y z $.  $d k m x y z $.
    hbsum1NEW.1 $e |- ( x e. A -> A. k x e. A ) $.
    $( Bound-variable hypothesis builder for sum. $)
    hbsum1NEW $p |- ( x e. sum_ k e. A B -> A. k x e. sum_ k e. A B ) $=
      ( vm vy vz csu cv cuz cfv wss caddc cop wcel cc0 cif wceq copab cz cres
      cseqz co cli wbr wa wrex cab cuni df-sumNEW ax-17 hbss hbopab1 hbres
      hbopr hbbr hban hbrex hbab hbuni hbxfr ) DABCDIBFJZKLZMZVCNOZGJDJBPCQRSZD
      GTZUAUBZUCUDZHJZUEUFZUGZFUAUHZHUIZUJHGBCDFUKDAVOVNDHAVMDFUAVCUAPDULVEVLDD
      ABVDEAJZVDPDULUMDAVJVKUEDAVFVIUCVPVFPDULVPUCPDULDAVHUAVGDGAUNVPUAPDULUOUP
      VPUEPDULVPVKPDULUQURUSUTVAVB $.
      $( [11-Dec-2005] $)
  $}

  ${
    $d A m w x y z $.  $d B m w y z $.  $d k m w x y z $.
    hbsumNEW.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbsumNEW.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for sum: if ` x ` is (effectively) not
       free in ` A ` and ` B ` , it is not free in ` sum_ k e. A B ` . $)
    hbsumNEW $p |- ( y e. sum_ k e. A B -> A. x y e. sum_ k e. A B ) $=
      ( vm vw vz csu cv cuz cfv wss caddc cop wcel cc0 cif wceq copab cz cres
      cseqz co cli wbr wa wrex cab cuni df-sumNEW ax-17 hbss hbif hbeq hbopab
      hbres hbopr hbbr hban hbrex hbab hbuni hbxfr ) ABCDEKCHLZMNZOZVGPQZILZELC
      RZDSTZUAZEIUBZUCUDZUEUFZJLZUGUHZUIZHUCUJZJUKZULJICDEHUMABWBWAAJBVTAHUCVGU
      CRAUNVIVSAABCVHFBLZVHRAUNUOABVQVRUGABVJVPUEWCVJRAUNWCUERAUNABVOUCVNEIABAB
      BVKVMWCVKRAUNVLABDSVLAUNGWCSRAUNUPUQURWCUCRAUNUSUTWCUGRAUNWCVRRAUNVAVBVCV
      DVEVF $.
      $( [11-Dec-2005] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d k x y z $.
    $( Equality theorem for sum. $)
    sumeq2NEW $p |- ( A. k e. A B = C -> sum_ k e. A B = sum_ k e. A C ) $=
      ( vz vy vx wceq wral cv cuz cfv wss caddc cop wcel cc0 cif copab cz cres
      cseqz co cli wbr wa wrex cab cuni csu hbra1 ax-17 ra4 imp iftrue adantl
      3eqtr4d ex wn iffalse eqtr4d pm2.61d eqeq2d opabbid reseq1 syl opreq2d
      breq1d anbi2d rexbidv abbidv unieqd df-sumNEW 3eqtr4g ) BCHZDAIZAEJZKLMZV
      QNOZFJZDJAPZBQRZHZDFSZTUAZUBUCZGJZUDUEZUFZETUGZGUHZUIVRVSVTWACQRZHZDFSZTU
      AZUBUCZWGUDUEZUFZETUGZGUHZUIABDUJACDUJVPWKWTVPWJWSGVPWIWRETVPWHWQVRVPWFWP
      WGUDVPWEWOVSUBVPWDWNHWEWOHVPWCWMDFVODAUKVPFULVPWBWLVTVPWAWBWLHZVPWAXAVPWA
      UFBCWBWLVPWAVOVODAUMUNWAWBBHVPWABQUOUPWAWLCHVPWACQUOUPUQURVPWAUSZXAXBXAVP
      XBWBQWLWABQUTWACQUTVAUPURVBVCVDWDWNTVEVFVGVHVIVJVKVLGFABDEVMGFACDEVMVN $.
      $( [11-Dec-2005] $)
  $}

  ${
    $d A j k m x y z $.  $d B m x y z $.  $d C m x y z $.
    cbvsumNEW.1 $e |- ( x e. B -> A. k x e. B ) $.
    cbvsumNEW.2 $e |- ( x e. C -> A. j x e. C ) $.
    cbvsumNEW.3 $e |- ( j = k -> B = C ) $.
    $( Change bound variable in a sum. $)
    cbvsumiNEW $p |- sum_ j e. A B = sum_ k e. A C $=
      ( vm vz vy cv cuz cfv wss caddc cop wcel cc0 cif wceq copab cz cres
      cseqz co cli wbr wa wrex cab cuni csu ax-17 hbif hbeq eleq1 eqidd
      ifbieq12d eqeq2d cbvopab1 reseq1 ax-mp opreq2i breq1i anbi2i rexbii
      abbii unieqi df-sumNEW 3eqtr4i ) BJMZNOPZVMQRZKMZEMZBSZCTUAZUBZEKUCZUDUEZ
      UFUGZLMZUHUIZUJZJUDUKZLULZUMVNVOVPFMZBSZDTUAZUBZFKUCZUDUEZUFUGZWDUHUIZUJZ
      JUDUKZLULZUMBCEUNBDFUNWHWSWGWRLWFWQJUDWEWPVNWCWOWDUHWBWNVOUFWAWMUBWBWNUBV
      TWLEKFFAAVPVSAMZVPSZFUOVRFACTVRFUOGWTTSZFUOUPUQEAAVPWKXAEUOWJEADTWJEUOHXB
      EUOUPUQVQWIUBZVSWKVPXCVRWJCTDTVQWIBURIXCTUSUTVAVBWAWMUDVCVDVEVFVGVHVIVJLK
      BCEJVKLKBDFJVKVL $.
      $( [11-Dec-2005] $)
  $}

  $(
  @{
    @d m n x y A @.  @d m n x y M @.  @d m n x y k @.
    @( Series sum with an infinite index set (i.e. an infinite series). @)
    dfisumNEW @p |- ( M e. ZZ -> sum_ k e. ( ZZ>= ` M ) A =
       U. { x |
    ( <. M , + >. seq ( { <. k , y >. | y = A } |` ZZ ) ) ~~> x } ) @=
      ? @.
      @( [11-Dec-2005] @)
  @}

  @{
    @d m n x y A @.  @d m n x y M @.  @d m n x y N @.  @d k m n x y @.
    @( Special case of series sum over a finite index set. @)
    dffsumNEW @p |- ( N e. ( ZZ>= ` M ) -> sum_ k e. ( M ... N ) A =
       ( ( <. M , + >. seq ( { <. k , y >. | y = A } |` ZZ ) ) ` N ) ) @=
      ? @.
      @( [11-Dec-2005] @)
  @}
  $)

  ${
    $d A a $.  $d A t y $.  $d A x y $.  $d r t $.
    $( Lemma for ~ gch-ac .  GCH implies that the power set of any ordinal is
       numerable. $)
    gch-aclem1 $p |- ( ( A. x e. On ( aleph ` suc x ) ~~ ~P ( aleph ` x ) /\
        A e. On ) -> E. y e. On y ~~ ~P A ) $=
      ( va cv csuc cale cfv cpw cen wbr con0 wral wcel wa ccrd wrex com crn wo
      cun wceq cardidm iscard3 mpbi elun wi cfn wss ssid ssnnfi mpan2 pwfi
      sylib finnum syl a1i wfn wb alephfnon fvelrnb ax-mp r19.29 alephon pweq
      breq2d biimpac breq1 rcla4ev sylancr r19.23aivw sylan2b ex adantr jaod
      mpi oncardid breq2 imbi12d visset pwen vtoclg mpd adantl entr expcom
      reximdv ) AEZFZGHZWHGHZIZJKZALMZCLNZOZBEZCPHZIZJKZBLQZWQCIZJKZBLQWPWRRNZW
      RGSZNZTZXAWRRXEUANZXGWRPHWRUBXHCUCWRUDUEWRRXEUFUEWPXDXAXFXDXAUGWPXDWSUHNZ
      XAXDWRUHNZXIXDWRWRUIXJWRUJWRWRUKULWRUMUNBWSUOUPUQWNXFXAUGWOWNXFXAXFWNWKWR
      UBZALQZXAGLURXFXLUSUTALWRGVAVBWNXLOWMXKOZALQXAWMXKALVCXMXAALXMWJLNWJWSJKZ
      XAWIVDXKWMXNXKWLWSWJJWKWRVEVFVGWTXNBWJLWQWJWSJVHVIVJVKUPVLVMVNVOVPWPWTXCB
      LWPWSXBJKZWTXCUGWOXOWNWOWRCJKZXOCVQWRDEZJKZWSXQIZJKZUGXPXOUGDCLXQCUBZXRXP
      XTXOXQCWRJVRYAXSXBWSJXQCVEVFVSWRXQDVTWAWBWCWDWTXOXCWQWSXBWEWFUPWGWC $.
      $( [10-Mar-2013] $)
  $}

  $(
  @{
    @( Lemma for ~ gch-ac . The limit step of the transfinite induction in the
       proof of ~ gch-ac . @)
    gch-aclem2 @p |- ( ( Lim r /\
      A. x e. On ( aleph ` suc x ) ~~ ~P ( aleph ` x ) /\
      A. s e. r E. t t We ( R1 ` s ) ) -> E. t t We ( R1 ` r ) ) @=
      ? @.
  @}

  @{
    @d r s t @.  @d r s x @.  @d r t y @.  @d s t u @.  @d t y z @.  @d u x @.
    @d x y @.
    @( The Generalized Continuum Hypothesis implies the Axiom of Choice (in the
       form of ~ numthcor ; this can be converted to more standard versions of
       the Axiom of Choice via ~ ween and ~ aceq8 . @)
    gch-ac @p |- ( A. x e. On ( aleph ` suc x ) ~~ ~P ( aleph ` x ) ->
        A. y E. z e. On z ~~ y ) @=
      ( vt vr vs vu cv csuc cale cfv cpw cen wbr con0 wral wrex crnk cr1 wwe
      wex wcel wi rankon c0 wceq wb fveq2 weeq2 syl exbidv weq we0 r10 ax-mp
      mpbir 19.8a a1i w3a ccrd cardon gch-aclem1 mpan2 3ad2ant2 cvv fvex ween
      cardid2 sylbi 3ad2ant3 pwen entr expcom 3syl r1suc breq2d 3ad2ant1
      sylibrd reximdv mpd sylibr 3exp wlim gch-aclem2 tfinds3 wss visset
      r1rankid wess mp2b eximi sylib 19.21aiv ) AHZIJKWNJKLMNAOPZCHBHZMNCOQZBWO
      WPRKZSKZDHZTZDUAZWQWROUBWOXBUCWPUDEHZSKZWTTZDUAZUESKZWTTZDUAZFHZSKZWTTZDU
      AZXJIZSKZWTTZDUAZXBWOEFWRXCUEUFZXEXHDXRXDXGUFXEXHUGXCUESUHXDXGWTUIUJUKEFU
      LZXEXLDXSXDXKUFXEXLUGXCXJSUHXDXKWTUIUJUKXCXNUFZXEXPDXTXDXOUFXEXPUGXCXNSUH
      XDXOWTUIUJUKXCWRUFZXEXADYAXDWSUFXEXAUGXCWRSUHXDWSWTUIUJUKXIWOXHXIXHUEWTTZ
      WTUMXGUEUFXHYBUGUNXGUEWTUIUOUPXHDUQUOURXJOUBZWOXMXQYCWOXMUSZGHZXOMNZGOQZX
      QYDYEXKUTKZLZMNZGOQZYGWOYCYKXMWOYHOUBYKXKVAAGYHVBVCVDYDYJYFGOYDYJYEXKLZMN
      ZYFYDYHXKMNZYIYLMNZYJYMUCXMYCYNWOXMYEXKMNGOQZYNXKVEUBXMYPUGXJSVFZGXKVEDVG
      UOGXKVHVIVJYHXKYQVKYJYOYMYEYIYLVLVMVNYCWOYFYMUGXMYCXOYLYEMXJVOVPVQVRVSVTX
      OVEUBXQYGUGXNSVFGXOVEDVGUOWAWBXCWCWOXMFXCPXFADFEWDWBWEUOXBWPWTTZDUAZWQXAY
      RDWPVEUBZWPWSWFXAYRUCBWGZWPVEWHWPWSWTWIWJWKYTYSWQUGUUACWPVEDVGUOWLUJWM @.
      @( [10-Mar-2013] @)
  @}
  $)

  $(
  @{
    @( The set of hereditarily finite sets is countable. @)
    r1om @p |- ( R1 ` om ) ~~ om @=
      ? @.
  @}

  @{
    @( The maximum of a finite collection of ordinals is in the set. @)
    ordunifi @p |- ( ( A C_ On /\ A e. Fin ) -> U. A e. A ) @=
      ? @.
  @}

  @{
    @( The set of hereditarily finite sets is a Tarski's class. (The
       Tarski-Grothendieck Axiom is not needed for this theorem.) @)
    r1omtar @p |- ( R1 ` om ) e. Tarski @=
      ? @.
  @}

  @{
    @d u x y f k @.
    @( A Grothendieck's universe is a set that is closed with respect to all
       the operations that are common in set theory: pairs, powersets, unions,
       intersections, cross products etc.  Grothendieck and alii,
       S&eacute;minaire de G&eacute;om&eacute;trie Alg&eacute;brique 4,
       Expos&eacute; I, p. 185.  It was designed to give a precise meaning to
       the concepts of categories of sets, groups... @)
    df-ungrNEW @a |- Uni = { u | ( Tr u /\ A. x e. u ( ~P x e. u /\
      A. y e. u { x , y } e. u /\ A. y e. ( u ^m x ) ran y e. u ) ) } @.
  @}
  $)

$( (End of Mario Carneiro's mathbox.) $)


