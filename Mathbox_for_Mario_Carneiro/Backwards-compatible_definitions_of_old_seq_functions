$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Backwards-compatible definitions of old "seq" functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g p $.
    $( Define the 1-based sequence in terms of a general sequence. $)
    df-seq1NEW $a |- seq1 = ( p e. _V , f e. _V |->
      ( seqNEW ( p , f ) ` 1 ) ) $.

    $( Define the 0-based sequence in terms of a general sequence. $)
    df-seq0NEW $a |- seq0 = ( p e. _V , f e. _V |->
      ( seqNEW ( p , f ) ` 0 ) ) $.

    $( Define the old general sequence from the new definition. $)
    df-seqzNEW $a |- seq = ( g e. _V , f e. _V |->
      ( seqNEW ( ( 2nd ` g ) , f ) ` ( 1st ` g ) ) ) $.
  $}

  ${
    $d F y $.  $d S x y $.
    seq1valNEW.1 $e |- S e. _V $.
    seq1valNEW.2 $e |- F e. _V $.
    $( Value of the 1-based recursive sequence builder operation.  (Revised by
       Mario Carneiro, 28-Jun-2013.) $)
    seq1fval $p |- ( S seq1 F ) = ( seqNEW ( S , F ) ` 1 ) $=
      ( vx vy cvv wcel cseq1 co c1 cseq cfv wceq cv seqeq1 fveq1d seqeq2
      df-seq1NEW fvex ovmpt2 mp2an ) AGHBGHABIJKABLZMZNCDEFABGGKEOZFOZLZMUDIKAU
      FLZMUEANKUGUHUEAUFPQUFBNKUHUCAUFBRQFESKUCTUAUB $.
      $( [28-Jun-2013] $) $( [3-Aug-2005] $)

    $( Value of the recursive sequence builder at 1.  See description in
       ~ df-seq1NEW .  (Revised by Mario Carneiro, 28-Jun-2013.) $)
    seq11NEW $p |- ( ( S seq1 F ) ` 1 ) = ( F ` 1 ) $=
      ( c1 cseq1 co cfv cseq seq1fval fveq1i cz wcel wceq 1z seq1NEW ax-mp
      eqtri ) EABFGZHEEABIHZHZEBHZESTABCDJKELMUAUBNOABEPQR $.
      $( [28-Jun-2013] $) $( [5-Oct-2004] $)

    $( Value of the recursive sequence builder at a successor.  See description
       in ~ df-seq1NEW .  (Revised by Mario Carneiro, 28-Jun-2013.) $)
    seq1p1NEW $p |- ( A e. NN -> ( ( S seq1 F ) ` ( A + 1 ) ) =
                ( ( ( S seq1 F ) ` A ) S ( F ` ( A + 1 ) ) ) ) $=
      ( cn wcel c1 caddc co cseq cfv cseq1 wceq cuz seqp1 nnuz eleq2s seq1fval
      fveq1i opreq1i 3eqtr4g ) AFGAHIJZHBCKLZLZAUDLZUCCLZBJZUCBCMJZLAUILZUGBJUE
      UHNAHOLFBCHAPQRUCUIUDBCDESZTUJUFUGBAUIUDUKTUAUB $.
      $( [28-Jun-2013] $) $( [5-Oct-2004] $)

    $( Functionality and domain of the recursive sequence builder.  (Revised by
       Mario Carneiro, 28-Jun-2013.) $)
    seq1fnNEW $p |- ( S seq1 F ) Fn NN $=
      ( cseq1 co cn wfn c1 cseq cfv cuz cz wcel 1z seqfn ax-mp seq1fval fneq1i
      nnuz fneq2i bitri mpbir ) ABEFZGHZIABJKZILKZHZIMNUHOABIPQUEUFGHUHGUDUFABC
      DRSGUGUFTUAUBUC $.
      $( [28-Jun-2013] $) $( [8-Oct-2004] $)
  $}

  ${
    $d F m y $.  $d M m $.  $d S m x y $.
    seq0valNEW.1 $e |- S e. _V $.
    seq0valNEW.2 $e |- F e. _V $.
    $( Value of the 0-based recursive sequence builder operation.  (Revised by
       Mario Carneiro, 28-Jun-2013.) $)
    seq0fvalNEW $p |- ( S seq0 F ) = ( seqNEW ( S , F ) ` 0 ) $=
      ( vx vy cvv wcel cseq0 co cc0 cseq cfv wceq cv seqeq1 fveq1d seqeq2
      df-seq0NEW fvex ovmpt2 mp2an ) AGHBGHABIJKABLZMZNCDEFABGGKEOZFOZLZMUDIKAU
      FLZMUEANKUGUHUEAUFPQUFBNKUHUCAUFBRQFESKUCTUAUB $.
      $( [28-Jun-2013] $) $( [3-Aug-2005] $)

    $( Value of the arbitrary-based recursive sequence builder operation.
       (Revised by Mario Carneiro, 28-Jun-2013.) $)
    seqzfvalNEW $p |- ( M e. ZZ ->
        ( <. M , S >. seq F ) = ( seqNEW ( S , F ) ` M ) ) $=
      ( vm vx vy cv cop cseqz co cseq cfv wceq cz opeq1 opreq1d fveq2 eqeq12d
      cvv wcel opex c1st c2nd visset op2nd syl6eq seqeq1 syl op1st fveq12d
      seqeq2 fveq1d df-seqzNEW fvex ovmpt2 mp2an vtoclg ) FIZAJZBKLZUTABMZNZOZC
      AJZBKLZCVCNZOFCPUTCOZVBVGVDVHVIVAVFBKUTCAQRUTCVCSTVAUAUBBUAUBVEUTAUCEGHVA
      BUAUAGIZUDNZVJUENZHIZMZNVDKUTAVMMZNVJVAOZVKUTVNVOVPVLAOVNVOOVPVLVAUENAVJV
      AUESUTAFUFZDUGUHVLAVMUIUJVPVKVAUDNUTVJVAUDSUTAVQUKUHULVMBOUTVOVCAVMBUMUNH
      GUOUTVCUPUQURUS $.
      $( [28-Jun-2013] $) $( [1-Sep-2005] $)

    $( Functionality and domain of a sequence generated by the arbitrary-based
       recursive sequence builder.  (Revised by Mario Carneiro,
       28-Jun-2013.) $)
    seqzfnNEW $p |- ( M e. ZZ ->
                 ( <. M , S >. seq F ) Fn ( ZZ>= ` M ) ) $=
      ( cz wcel cop cseqz co cuz cfv wfn cseq seqfn seqzfvalNEW fneq1d mpbird
      ) CFGZCAHBIJZCKLZMCABNLZUAMABCOSUATUBABCDEPQR $.
      $( [28-Jun-2013] $) $( [2-Sep-2005] $)

    $( The 1-based recursive sequence in terms of the arbitrary-based one.
       (Revised by Mario Carneiro, 28-Jun-2013.) $)
    seq1seqzNEW $p |- ( S seq1 F ) = ( <. 1 , S >. seq F ) $=
      ( cseq1 co c1 cseq cfv cop cseqz seq1fval cz wcel wceq 1z seqzfvalNEW
      ax-mp eqtr4i ) ABEFGABHIZGAJBKFZABCDLGMNUATOPABGCDQRS $.
      $( [28-Jun-2013] $) $( [1-Sep-2005] $)

    $( The 0-based recursive sequence in terms of the arbitrary-based one.
       (Revised by Mario Carneiro, 28-Jun-2013.) $)
    seq0seqzNEW $p |- ( S seq0 F ) = ( <. 0 , S >. seq F ) $=
      ( cseq0 co cc0 cseq cfv cop cseqz seq0fvalNEW cz wcel wceq 0z
      seqzfvalNEW ax-mp eqtr4i ) ABEFGABHIZGAJBKFZABCDLGMNUATOPABGCDQRS $.
      $( [28-Jun-2013] $) $( [1-Sep-2005] $)

    $( Functionality and domain of the 0-based recursive sequence builder.
       (Revised by Mario Carneiro, 28-Jun-2013.) $)
    seq0fnNEW $p |- ( S seq0 F ) Fn NN0 $=
      ( cseq0 co cn0 wfn c1 cshi cseq1 cneg cres cc wss cvv wcel negex oprex
      shftfn ax-mp nn0sscn fnssres mp2an seq0fval fneq1i mpbir ) ABEFZGHABIJFZK
      FZILZJFZGMZGHZULNHZGNOUNUKPQUOIRUKPUJAUIKSTUAUBNGULUCUDGUHUMABCDUEUFUG $.
      $( [28-Jun-2013] $) $( [3-Aug-2005] $)

    $( Value of the arbitrary-based recursive sequence builder at its initial
       value.  (Revised by Mario Carneiro, 28-Jun-2013.) $)
    seqz1NEW $p |- ( M e. ZZ -> ( ( <. M , S >. seq F ) ` M ) = ( F ` M ) ) $=
      ( cz wcel cop cseqz co cfv cseq seqzfvalNEW fveq1d seq1NEW eqtrd ) CFGZCC
      AHBIJZKCCABLKZKCBKQCRSABCDEMNABCOP $.
      $( [28-Jun-2013] $) $( [1-Sep-2005] $)

    $( Value of the arbitrary-based recursive sequence builder at a successor
       value.  (Revised by Mario Carneiro, 28-Jun-2013.) $)
    seqzp1NEW $p |- ( N e. ( ZZ>= ` M ) ->
                 ( ( <. M , S >. seq F ) ` ( N + 1 ) ) =
                 ( ( ( <. M , S >. seq F ) ` N ) S ( F ` ( N + 1 ) ) ) ) $=
      ( cuz cfv wcel c1 caddc co cseq cop cseqz seqp1 cz wceq eluzel2
      seqzfvalNEW syl fveq1d opreq1d 3eqtr4d ) DCGHIZDJKLZCABMHZHDUGHZUFBHZALUF
      CANBOLZHDUJHZUIALABCDPUEUFUJUGUECQIUJUGRCDSABCEFTUAZUBUEUKUHUIAUEDUJUGULU
      BUCUD $.
      $( [28-Jun-2013] $) $( [7-Sep-2005] $)

    $( Value of the 0-based recursive sequence builder at 0.  (Revised by Mario
       Carneiro, 28-Jun-2013.) $)
    seq00NEW $p |- ( ( S seq0 F ) ` 0 ) = ( F ` 0 ) $=
      ( cc0 cseq0 co cfv cseq seq0fvalNEW fveq1i cz wcel wceq 0z seq1NEW ax-mp
      eqtri ) EABFGZHEEABIHZHZEBHZESTABCDJKELMUAUBNOABEPQR $.
      $( [28-Jun-2013] $) $( [3-Aug-2005] $)

    $( Value of the 0-based recursive sequence builder at a successor.
       (Revised by Mario Carneiro, 28-Jun-2013.) $)
    seq0p1NEW $p |- ( N e. NN0 -> ( ( S seq0 F ) ` ( N + 1 ) ) =
                ( ( ( S seq0 F ) ` N ) S ( F ` ( N + 1 ) ) ) ) $=
      ( cn0 wcel c1 caddc co cc0 cseq cfv cseq0 wceq cuz seqp1 nn0uz eleq2s
      seq0fvalNEW fveq1i opreq1i 3eqtr4g ) CFGCHIJZKABLMZMZCUEMZUDBMZAJZUDABNJZ
      MCUJMZUHAJUFUIOCKPMFABKCQRSUDUJUEABDETZUAUKUGUHACUJUEULUAUBUC $.
      $( [28-Jun-2013] $) $( [3-Aug-2005] $)
  $}

