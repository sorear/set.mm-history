$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                                 Integer powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Define exponentiation to nonnegative integer powers.  This definition is
       not meant to be used directly; instead, ~ exp0 and ~ expp1 provide a the
       standard recursive definition.  The up-arrow notation is used by Donald
       Knuth for iterated exponentiation (_Science_ 194, 1235-1242, 1976) and
       is convenient for us since we don't have superscripts.  See ~ expnnval
       for a description of how the recursive sequence builder is used.
       10-Jun-2005:  The definition was extended to include zero exponents, so
       that ` 0 ^ 0 = 1 ` per the convention of Definition 10-4.1 of [Gleason]
       p. 134.  (Based on definition contributed by Raph Levien,
       15-Oct-2004.) $)
    df-expNEW $a |- ^ = ( x e. CC , y e. NN0 |-> ( (
      seqNEW ( x. , ( { <. 0 , 1 >. } u. ( NN X. { x } ) ) ) ` 0 ) ` y ) ) $.
  $}

  ${
    $d x y A $.  $d y N $.
    $( Value of exponentiation to nonnegative integer powers.  (Revised by
       Mario Carneiro, 2-Jul-2013.) $)
    expvalNEW $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ N ) = ( (
      seqNEW ( x. , ( { <. 0 , 1 >. } u. ( NN X. { A } ) ) ) ` 0 ) ` N ) ) $=
      ( vx vy cc cn0 cv cc0 cmul c1 cop csn cn cxp cun cseq cfv cexp wceq sneq
      xpeq2d uneq2d seqeq2 syl fveq1d fveq2 df-expNEW fvex ovmpt2 ) CDABEFDGZHI
      HJKLZMCGZLZNZOZPZQZQBHIUKMALZNZOZPZQZQRUJVBQULASZUJUQVBVCHUPVAVCUOUTSUPVA
      SVCUNUSUKVCUMURMULATUAUBIUOUTUCUDUEUEUJBVBUFCDUGBVBUHUI $.
      $( [2-Jul-2013] $) $( [20-May-2004] $)

    $( Value of a complex number raised to the 0th power.  Note that under our
       definition, ` 0 ^ 0 = 1 ` , following the convention used by Gleason.
       Part of Definition 10-4.1 of [Gleason] p. 134.  (Revised by Mario
       Carneiro, 2-Jul-2013.) $)
    exp0NEW $p |- ( A e. CC -> ( A ^ 0 ) = 1 ) $=
      ( cc wcel cc0 cexp co cmul c1 cop csn cn cxp cun cseq cfv cn0 wceq 0nn0
      expvalNEW mpan2 cz 0z seq1NEW ax-mp elisseti ax-1cn cdif cres wn 0nnn
      difsn reseq2i wrel cdm wss relxp dmxpss relssres mp2an eqtr2i uneq2i
      fvsnun1 eqtri syl6eq ) ABCZADEFZDDGDHIJZKAJZLZMZNOOZHVEDPCVFVKQRADSTVKDVJ
      OZHDUACVKVLQUBGVJDUCUDDHKVIVJDUAUBUEHBUFUEVIVIKDJUGZUHZVGVNVIKUHZVIVMKVID
      KCUIVMKQUJDKUKUDULVIUMVIUNKUOVOVIQKVHUPKVHUQVIKURUSUTVAVBVCVD $.
      $( [2-Jul-2013] $) $( [20-May-2004] $)
  $}

  $( Value of a complex number raised to a nonnegative integer power plus one.
     Part of Definition 10-4.1 of [Gleason] p. 134.  (Revised by Mario
     Carneiro, 2-Jul-2013.) $)
  expp1NEW $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ ( N + 1 ) ) =
               ( ( A ^ N ) x. A ) ) $=
    ( cc wcel cn0 wa c1 caddc co cc0 cmul cop csn cn cxp cun cseq cfv cexp
    wceq cuz seqp1 nn0uz eleq2s adantl peano2nn0 expvalNEW sylan2 cdif nn0p1nn
    wn 0nnn difsn ax-mp syl6eleqr 0cn elisseti ax-1cn cres reseq2i wrel cdm
    wss relxp dmxpss relssres mp2an eqtr2i uneq2i fvsnun2 syl fvconst2g eqtr2d
    opreq12d 3eqtr4d ) ACDZBEDZFZBGHIZJKJGLMZNAMZOZPZQRZRZBWDRZVSWCRZKIZAVSSIZA
    BSIZAKIVQWEWHTZVPWKBJUAREKWCJBUBUCUDUEVQVPVSEDWIWETBUFAVSUGUHVRWJWFAWGKABUG
    VRWGVSWBRZAVQWGWLTZVPVQVSNJMUIZDWMVQVSNWNBUJZJNDUKWNNTULJNUMUNZUOJGNVSWBWCJ
    CUPUQGCURUQWBWBWNUSZVTWQWBNUSZWBWNNWBWPUTWBVAWBVBNVCWRWBTNWAVDNWAVEWBNVFVGV
    HVIVJVKUEVQVPVSNDWLATWONAVSCVLUHVMVNVO $.
    $( [2-Jul-2013] $) $( [20-May-2005] $)

  $( Value of a complex number raised to the first power.  (Revised by Mario
     Carneiro, 2-Jul-2013.) $)
  exp1NEW $p |- ( A e. CC -> ( A ^ 1 ) = A ) $=
    ( cc wcel c1 cexp co cc0 caddc wceq ax-1cn addid2 ax-mp opreq2i cmul cn0
    0nn0 expp1NEW mpan2 exp0NEW opreq1d mulid2 3eqtrd syl5eqr ) ABCZADEFAGDHFZE
    FZAUEDAEDBCUEDIJDKLMUDUFAGEFZANFZDANFAUDGOCUFUHIPAGQRUDUGDANASTAUAUBUC $.
    $( [2-Jul-2013] $) $( [20-Oct-2004] $)

  $( Distribution of square over division.  (Contributed by Mario Carneiro,
     9-Jul-2013.) $)
  sqdiv $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
      ( ( A / B ) ^ 2 ) = ( ( A ^ 2 ) / ( B ^ 2 ) ) ) $=
    ( cc wcel cc0 wne w3a cdiv co cmul c2 cexp wa wceq simp1 3simpc divmuldiv
    syl22anc divcl sqval syl opreq12 syl2an 3adant3 3eqtr4d ) ACDZBCDZBEFZGZABH
    IZUJJIZAAJIZBBJIZHIZUJKLIZAKLIZBKLIZHIZUIUFUFUGUHMZUSUKUNNUFUGUHOZUTUFUGUHP
    ZVAAABBQRUIUJCDUOUKNABSUJTUAUFUGURUNNZUHUFUPULNUQUMNVBUGATBTUPULUQUMHUBUCUD
    UE $.
    $( [9-Jul-2013] $) $( [20-Aug-2001] $)

