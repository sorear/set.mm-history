$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
       Grammars, Logics, Machines and Automata
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c Kleene $.

  $( Extend class notation with the kleene star. $)
  ckln $a class Kleene $.

  $c Words $.

  $( Extend class notation with the class of words of a given size. $)
  cwrd $a class Words $.

  ${
    $d x y $.
    $( The words of size ` y ` over an alphabet ` x ` are the finite sequences
       over ` x ` of size ` y ` .  Their domains are fiercely set to
       ` ( 1 ... y ) ` so that I can concatenate them easily.  The case
       ` y = 0 ` is a bit tricky and corresponds to the unique empty word
       (often denoted by an epsilon or by ` 1 ` in textbooks.)
       Experimental. $)
    df-words $a |- Words = ( x e. _V , y e. NN0 |-> ( x ^m ( 1 ... y ) ) ) $.
  $}

  ${
    $d x y A $.  $d y N $.
    $( The words over a set ` A ` .  (Contributed by FL, 14-Jan-2014.) $)
    isword $p |- ( ( A e. B /\ N e. NN0 )
      -> ( A Words N ) = ( A ^m ( 1 ... N ) ) ) $=
      ( vx vy wcel cn0 wa cvv c1 cfz co cmap cwrd elisset adantr simpr oprex cv
      wceq a1i opreq1 opreq2 opreq2d df-words ovmpt2g syl111anc ) ABFZCGFZHZAIF
      ZUIAJCKLZMLZIFZACNLUMTUHUKUIABOPUHUIQUNUJAULMRUADEACIGDSZJESZKLZMLUMNAUQM
      LIUOAUQMUBUPCTUQULAMUPCJKUCUDDEUEUFUG $.
      $( [14-Jan-2014] $)
  $}

  ${
    $d x a u $.
    $( The Kleene star of an alphabet ` x ` is the set of all the finite
       sequences of elements of this alphabet.  Experimental. $)
    df-kle $a |- Kleene = ( x e. _V |->
      U_ u e. NN0 ( x ^m ( 1 ... u ) ) ) $.
  $}

  ${
    $d x u A $.
    $( The predicate is the Kleene star of a set ` A ` .  (Contributed by FL,
       14-Jan-2014.) $)
    isKleene $p |- ( A e. B
    -> ( Kleene ` A ) = U_ u e. NN0 ( A ^m ( 1 ... u ) ) ) $=
      ( vx wcel cvv cn0 c1 cv cfz co cmap ciun ckln cfv wceq elisset wral nn0ex
      a1i oprex iunexg sylancr opreq1 adantr ralrimiva iuneq2 syl df-kle fvmptg
      rgen syl11anc ) BCEZBFEAGBHAIZJKZLKZMZFEZBNOUQPBCQUMGFEUPFEZAGRZURSUTUMUS
      AGUSUNGEZBUOLUATUKTAGUPFFUBUCDBAGDIZUOLKZMZUQFFNVBBPZVCUPPZAGRVDUQPVEVFAG
      VEVFVAVBBUOLUDUEUFAGVCUPUGUHDAUIUJUL $.
      $( [14-Jan-2014] $)
  $}

  ${
    $d x C $.
    selsubf.1 $e |- A e. _V $.
    selsubf.2 $e |- C e. _V $.
    $( A way of selecting a subset of functions so that their values belong to
       ` B ` .  (Contributed by FL, 14-Jan-2014.) $)
    selsubf $p |- ( ( A ^m C ) i^i ~P ( C X. B ) ) = ( ( A i^i B ) ^m C ) $=
      ( vx cxp cpw cv wfn cab cin cmap co in32 xpindi pweqi pwin ineq1i mapval2
      eqtr2i eqtri inex1 3eqtr4i ) CAGZHZFICJFKZLZCBGZHZLZCABLZGZHZUGLZACMNZUJL
      ULCMNUKUFUJLZUGLUOUFUGUJOUQUNUGUNUEUILZHUQUMURCABPQUEUIRUASUBUPUHUJACFDET
      SULCFABDUCETUD $.
      $( [14-Jan-2014] $)
  $}

  ${
    $d f C $.
    selsubf3.1 $e |- A e. _V $.
    selsubf3.2 $e |- C e. _V $.
    $( A way of selecting a subset of functions so that their values belong to
       ` B ` .  (Contributed by FL, 14-Jan-2014.) $)
    selsubf3 $p |- ( ( A ^m C ) i^i ~P ( _V X. B ) ) = ( ( A i^i B ) ^m C ) $=
      ( vf cxp cpw cv wfn cab cin cvv cmap co in32 pwin eqtri ineq1i mapval2
      inxp inv1 xpeq1i pweqi eqtr3i inex1 3eqtr4i ) CAGZHZFICJFKZLZMBGZHZLZCABL
      ZGZHZUJLZACNOZUMLUOCNOUNUIUMLZUJLURUIUJUMPUTUQUJUHULLZHUTUQUHULQVAUPVACML
      ZUOGUPCAMBUAVBCUOCUBUCRUDUESRUSUKUMACFDETSUOCFABDUFETUG $.
      $( [14-Jan-2014] $)
  $}

  ${
    $( A way of selecting a subset of functions so that their values belong to
       ` B ` .  (Contributed by FL, 14-Jan-2014.) $)
    selsubf3g $p |- ( ( A e. D /\ C e. E ) ->
        ( ( A ^m C ) i^i ~P ( _V X. B ) ) = ( ( A i^i B ) ^m C ) ) $=
      ( wcel cvv cmap cxp cin wceq elisset cif ineq1d eqeq12d opreq2 0ex elimel
      co c0 cpw opreq1 ineq1 opreq1d selsubf3 dedth2h syl2an ) ADFAGFZCGFZACHSZ
      GBIUAZJZABJZCHSZKZCEFADLCELUHUIUOUHATMZCHSZUKJZUPBJZCHSZKUPUICTMZHSZUKJZU
      SVAHSZKACTTAUPKZULURUNUTVEUJUQUKAUPCHUBNVEUMUSCHAUPBUCUDOCVAKZURVCUTVDVFU
      QVBUKCVAUPHPNCVAUSHPOUPBVAATGQRCTGQRUEUFUG $.
      $( [14-Jan-2014] $)
  $}

  $c IndCls $.

  $( Extend class notation with the class of inductive closures. $)
  clincl $a class IndCls $.

  $c IndClsBu $.

  $( Extend class notation with the class of inductive closures. $)
  clinclb $a class IndClsBu $.

  ${
    $d x y a f j $.
    $( Definition of an inductive closure.  Top down definition.  Gallier
       p. 19 $)
    df-indcls $a |- IndCls = ( x e. _V , y e. _V |-> |^| { a | ( x C_ a /\
        A. f e. y A. j e. ( dom f i^i ~P ( _V X. a ) ) ( f ` j ) e. a ) } ) $.
  $}

  ${
    $( Remove F e. B $)
    $d F x y f j a $.  $d X x y f j a $.  $d A f j n $.  $d B f j n $.
    $d U f j n $.  $d U j a $.  $d F n $.  $d X n $.
    $( The inductive closure of ` X ` under ` F ` .  (Contributed by FL,
       14-Jan-2014.) $)
    indcls $p |- ( ( ( U e. A /\ X C_ U /\ F e. B ) /\
        A. f e. F E. n e. NN f e. ( U ^m ( U ^m ( 1 ... n ) ) ) )
      -> ( X IndCls F ) = |^| { a | ( X C_ a /\
        A. f e. F A. j e. ( dom f i^i ~P ( _V X. a ) )
          ( f ` j ) e. a ) } ) $=
      ( vy wcel cv co cmap wral wa cvv cin wceq adantr vx wss w3a c1 cfz cn cfv
      wrex cdm cxp cpw cab cint clincl ssexg ancoms 3adant3 elisset 3ad2ant3 c0
      wne wex simpl1 simpl2 wi wf wb simp1 oprex elmapg sylancl jctir selsubf3g
      fdm adantl syl eleq2d ffvelrn com12 inidm opreq1i eleq2s pm2.43a ralrimiv
      ex syl6bi ineq1 raleq syl5ibr mpcom pm2.43b rexlimivw ralimdv sseq2 xpeq2
      imbi2d imp pweqd ineq2d imbi12d ralbidv2 ralbidv anbi12d cla4egv syl12anc
      eleq2 sylibr intex sylib sseq1 wel simpr imbi1d abbidv df-indcls ovmpt2ga
      abn0 inteqd syl111anc ) CAKZHCUBZGBKZUCZDLZCCUDFLZUEMZNMZNMKZFUFUHZDGOZPZ
      HQKZGQKZHILZUBZELZYDUGZYNKZEYDUIZQYNUJZUKZRZOZDGOZPZIULZUMZQKZHGUNMUUGSYC
      YLYJXTYAYLYBYAXTYLHCAUOUPUQTYCYMYJYBXTYMYAGBURUSTYKUUFUTVAZUUHYKUUEIVBZUU
      IYKXTYAYQCKZEYSQCUJZUKZRZOZDGOZUUJXTYAYBYJVCXTYAYBYJVDYCYJUUPYCYIUUODGYIY
      CUUOYHYCUUOVEZFUFYHYCUUOYCYHYGCYDVFZUUQYCXTYGQKYHUURVGXTYAYBVHZCYFNVICYGY
      DAQVJVKYSYGSZUURUUQYGCYDVNUURUUQUUTYCUUKEYGUUMRZOZVEUURYCUVBUURYCPZUUKEUV
      AYPUVAKZUVCUUKUVCUVDYPCCRZYFNMZKUVCUUKVEZUVCUVAUVFYPUVCXTYFQKZPZUVAUVFSYC
      UVIUURYCXTUVHUUSUDYEUEVIVLVOCCYFAQVMVPVQUVGYPYGUVFUVCYPYGKZUUKUURUVJUUKVE
      YCUURUVJUUKYGCYPYDVRWETVSUVECYFNCVTWAWBWFWCWDWEUUTUUOUVBYCUUTUUNUVASUUOUV
      BVGYSYGUUMWGUUKEUUNUVAWHVPWPWIWJWFWKWLVSWMWQXTYAUUPPZUUJUUEUVKICAYNCSZYOY
      AUUDUUPYNCHWNUVLUUCUUODGUVLYRUUKEUUBUUNUVLYPUUBKYPUUNKYRUUKUVLUUBUUNYPUVL
      UUAUUMYSUVLYTUULYNCQWOWRWSVQYNCYQXFWTXAXBXCXDWQXEUUEIXQXGUUFXHXIUAJHGQQUA
      LZYNUBZUUCDJLZOZPZIULZUMUUGUNQUVMHSZUVOGSZPZUVRUUFUWAUVQUUEIUWAUVNYOUVPUU
      DUVSUVNYOVGUVTUVMHYNXJTUWAUUCUUCDUVOGUWADJXKYDGKUUCUWAUVOGYDUVSUVTXLVQXMX
      AXCXNXRUAJDEIXOXPXS $.
      $( [14-Jan-2014] $)
  $}

  ${
    $d u x y z n a l m b o f j $.
    $( Definition of an inductive closure.  Gallier p. 19.  Bottom up
       definition. $)
    df-indclsbu $a |- IndClsBu = ( u e. _V |-> ( x e. ~P u ,
      y e. ~P { z | E. n e. NN z e. ( u ^m ( u ^m ( 1 ... n ) ) ) }
        |-> ( a e. NN0 |->
           ( ( ( l e. ~P u , m e. ~P u |-> ( l u. m ) ) seq0 ( b e. NN0 |->
         if ( b = 0 , x ,
            { o | E. f e. y E. j e. ( dom f i^i
              ( _V X. ( ( x IndClsBu y ) ` ( b - 1 ) ) ) )
              o = ( f ` j ) } ) ) ) ` a ) ) ) ) $.
  $}

  ${
    $d A f n $.  $d f U n $.  $d f F $.
    $( Lemma for ~ indclsbu .  (Contributed by FL, 14-Jan-2014.) $)
    lemindclsbu $p |- ( ( U e. A /\
      A. f e. F E. n e. NN f e. ( U ^m ( U ^m ( 1 ... n ) ) ) )
       -> F e. _V ) $=
      ( wcel c1 co cmap cn cz cxp cpw wss cvv wa wi sylbi syl mp2b cv wrex wral
      cfz wex df-rex oprex mapsspw cuz fzssuz uzssz sstri xpss1 sspwb sstr ssel
      cfv w3a a1i syl5com ex com5l 3imp1 com12 3exp1 expcom imp exlimiv ralimdv
      a1dd dfss3 sylibr elisset xpexg pwexg sylancr syl11anc ssexg mpan9 syldan
      zex ) BAFZCUAZBBGDUAZUDHZIHZIHZFZDJUBZCEUCZEKBLZMZBLZMZNZEOFZWBWJPWCWNFZC
      EUCZWOWBWJWRWBWIWQCEWIWBWQWIWDJFZWHPZDUEWBWQQZWHDJUFWTXADWSWHXAWEOFWFWEBL
      ZMZNZWSWHXAQZQZGWDUDUGBWEOUHWEKNXBWKNZXDXFQZWEGUIUQKGWDUJGUKULWEKBUMXGXCW
      LNZXHXBWKUNXDXIXFXDXIPWFWLNZXFWFXCWLUOXJWSWHWBWQWFOFWGWFBLZMZNZXJWSWHURWB
      PZWQQBWEIUGBWFOUHXNXMWQXJWSWHWBXMWQQZXJXKWMNZWSWHWBXOQQQZWFWLBUMXPXLWNNZX
      QXKWMUNXMXRWSWHWBWQXMXRXFXMXRPWGWNNZWSXEWGXLWNUOXSXEQWSXSWHWQWBWGWNWCUPVJ
      USUTVAVBRSVCVDTVESVFRTTVGVHRVDVIVGCEWNVKVLWBWNOFZWOWPWBWMOFZXTWBWLOFZBOFZ
      YAWBKOFZYCYBWABAVMZYDYCPWKOFYBKBOOVNWKOVOSVPYEWLBOOVNVQWMOVOSWOXTWPEWNOVR
      VAVSVT $.
      $( [14-Jan-2014] $)
  $}

  ${
    $d a b f j l m n o u x y z $.  $d a b f j o x y z F $.  $d a f j o N $.
    $d a f l m n u x y z U $.  $d a b f j o x y X $.  $d f n A $.
    $( Definition of an inductive closure.  Gallier p. 19.  (Contributed by FL,
       14-Jan-2014.) $)
    indclsbu $p |- ( ( ( U e. A /\ X C_ U ) /\
      A. f e. F E. n e. NN f e. ( U ^m ( U ^m ( 1 ... n ) ) ) /\ N e. NN0 ) ->
        ( ( X ( IndClsBu ` U ) F ) ` N ) = ( ( ( l e. ~P U , m e. ~P U |->
      ( l u. m ) ) seq0 ( b e. NN0 |-> if ( b = 0 , X ,
        { o | E. f e. F E. j e. ( dom f i^i
          ( _V X. ( ( X IndClsBu F ) ` ( b - 1 ) ) ) )
         o = ( f ` j ) } ) ) ) ` N ) ) $=
      ( vz wcel cv co cn0 cfv wceq cvv vx vy va vu wss wa c1 cfz cmap wrex wral
      w3a clinclb cpw cab cun cmpt2 cc0 cdm cmin cxp cin cif cmpt cseq0 elisset
      cn adantr 3ad2ant1 pwexg nnex a1i abid2 oprex eqeltri rgen abrexex2g 3syl
      jctir mpt2exg syl11anc pweq id opreq1 opreq12d eleq2d rexbidv pweqd eqidd
      abbidv mpt2eq123dv fveq1d mpteq2dva df-indclsbu fvmptg opreqd ssexg elpwg
      opreq1d ancoms biimprd adantld mpcom simpl eleq1d rexbidva cbvralv biimpi
      weq 3ad2ant2 ssabral sylibr lemindclsbu adantlr 3adant3 syl mpbird mptexg
      nn0ex ax-mp biidd simpll wel simplr opreq12 xpeq2d ineq2d anbi1d rexbidv2
      wb anbi12d ifbieq12d opreq2d eqid ovmpt2ga syl111anc simp3 fveq2 sylancl
      fvex 3eqtrd ) BANZJBUEZUFZCOZBBUGFOZUHPZUIPZUIPZNZFVGUJZCHUKZIQNZULZIJHBU
      MRZPZRIJHUAUBBUNZMOZUUINZFVGUJZMUOZUNZUCQUCOZLEUUQUUQLOEOUPZUQZKQKOZURSZU
      AOZGODOZUUERSZDUUEUSZTUVFUGUTPZUVHUBOZUMPZRZVAZVBZUJZCUVMUJZGUOZVCZVDZVEP
      ZRZVDZUQZPZRIUCQUVCUVEKQUVGJUVJDUVKTUVLJHUMPZRZVAZVBZUJZCHUJZGUOZVCZVDZVE
      PZRZVDZRZIUWQRZUUNIUUPUWGUUNUUOUWFJHUUNBTNZUWFTNZUUOUWFSUUDUULUXBUUMUUBUX
      BUUCBAVFVHVIUUNUUQTNZUVBTNZUXCUUDUULUXDUUMUUBUXDUUCBAVJVHVIUUNVGTNZUUSMUO
      ZTNZFVGUKZUFUVATNUXEUUNUXFUXIUXFUUNVKVLUXHFVGUXHUUFVGNZUXGUUITMUUIVMBUUHU
      IVNVOVLVPVSUUSFMVGTTVQUVATVJVRUAUBUUQUVBUWETTVTWAUDBUAUBUDOZUNZUURUXKUXKU
      UGUIPZUIPZNZFVGUJZMUOZUNZUCQUVCLEUXLUXLUVDUQZUWBVEPZRZVDZUQUWFTTUMUXKBSZU
      AUBUXLUXRUYBUUQUVBUWEUXKBWBZUYCUXQUVAUYCUXPUUTMUYCUXOUUSFVGUYCUXNUUIUURUY
      CUXKBUXMUUHUIUYCWCUXKBUUGUIWDWEWFWGWJWHUYCUCQUYAUWDUYCUYAUWDSUVCQNZUYCUVC
      UXTUWCUYCUXSUVEUWBVEUYCLEUXLUXLUVDUUQUUQUVDUYDUYDUYCUVDWIWKWSWLVHWMWKUAUB
      MUDCDEFGUCKLWNWOWAWPWLUUNIUWGUWSUUNJUUQNZHUVBNZUWSTNZUWGUWSSUUDUULUYFUUMJ
      TNZUUDUYFUUCUUBUYIJBAWQWTUYIUUCUYFUUBUYIUYFUUCJBTWRXAXBXCVIUUNUYGHUVAUEZU
      UNUUTMHUKZUYJUULUUDUYKUUMUULUYKUUKUUTCMHCMXIZUUJUUSFVGUYLUXJUFUUEUURUUIUY
      LUXJXDXEXFXGXHXJUUTMHXKXLUUNHTNZUYGUYJYJUUDUULUYMUUMUUBUULUYMUUCABCFHXMXN
      XOHUVATWRXPXQUYHUUNQTNUYHXSUCQUWRTXRXTVLUAUBJHUUQUVBUWEUWSUWFTUVHJSZUVMHS
      ZUFZUCQUWDUWRUYPUWDUWRSUYEUYPUVCUWCUWQUYPUWBUWPUVEVEUYPKQUWAUWOUYPUVFQNZU
      FZUVGUVGUVHUVTJUWNUYRUVGYAUYNUYOUYQYBUYRUVSUWMGUYRUVRUWLCUVMHUYRCUBYCUUEH
      NUVRUWLUYRUVMHUUEUYNUYOUYQYDWFUYRUVJUVJDUVQUWKUYRUVIUVQNUVIUWKNUVJUYRUVQU
      WKUVIUYRUVPUWJUVKUYRUVOUWITUYRUVLUVNUWHUYPUVNUWHSUYQUVHJUVMHUMYEVHWLYFYGW
      FYHYIYKYIWJYLWMYMWLVHWMUWFYNYOYPWLUUNUUMUXATNUWTUXASUUDUULUUMYQIUWQYTUCIU
      WRUXAQTUWSUVCIUWQYRUWSYNWOYSUUA $.
      $( [14-Jan-2014] $)
  $}

  ${
    $d b f j l m n o $.  $d b f j o F $.  $d b f j o X $.  $d f n A $.
    $d f l m n U $.
    $( Definition of an inductive closure at ` 0 ` .  (Contributed by FL,
       14-Jan-2014.) $)
    indclsbu0 $p |- ( ( U e. A /\ X C_ U /\
      A. f e. F E. n e. NN f e. ( U ^m ( U ^m ( 1 ... n ) ) ) ) ->
        ( ( X ( IndClsBu ` U ) F ) ` 0 ) = X ) $=
      ( vl vm vb vo vj wcel cv co wrex cc0 cfv cn0 wceq cvv wss c1 cmap cn wral
      cfz w3a clinclb cpw cun cmpt2 cdm cmin cxp cin cab cif cseq0 3simpa simp3
      cmpt wa 0nn0 a1i indclsbu syl111anc pwexg mpt2exg syl11anc 3ad2ant1 nn0ex
      mptexg syl seq00g ssexg 3adant3 lemindclsbu 3adant2 dmexg rgen ab2rexexg2
      ancoms inex1g ifcl eqeq1 opreq1 adantr fveq2d xpeq2d ineq2d eleq2d anbi1d
      sylancl rexbidv2 rexbidva abbidv ifbieq2d eqid fvmptg iftrue ax-mp syl6eq
      sylancr 3eqtrd ) BALZFBUAZCMZBBUBDMUFNUCNUCNLDUDOCEUEZUGZPFEBUHQNQZPGHBUI
      ZXKGMHMUJZUKZIRIMZPSZFJMKMZXGQZSZKXGULZTXNUBUMNZFEUHNZQZUNZUOZOZCEOZJUPZU
      QZVAZURNQZPYIQZFXIXEXFVBXHPRLZXJYJSXEXFXHUSXEXFXHUTYLXIVCVDABCKHDJEPFIGVE
      VFXIXMTLZYITLZYJYKSXEXFYMXHXEXKTLZYOYMBAVGZYPGHXKXKXLTTVHVIVJXIRTLZYNYQXI
      VKVDIRYHTVLVMTTXMYIVNVIXIYKPPSZFXRKXSTPUBUMNZYAQZUNZUOZOZCEOZJUPZUQZFXIYL
      UUFTLZYKUUFSVCXIFTLZUUETLZUUGXEXFUUHXHXFXEUUHFBAVOWBVPXIETLZUUBTLZCEUEUUI
      XEXHUUJXFABCDEVQVRUUKCEXGELZXSTLUUKXGEVSXSUUATWCVMVTCKJEUUBXQTTWAWMYRFUUE
      TWDVIIPYHUUFRTYIXOXOYRYGUUEFXNPPWEXOYFUUDJXOYEUUCCEXOUULVBZXRXRKYDUUBUUMX
      PYDLXPUUBLXRUUMYDUUBXPUUMYCUUAXSUUMYBYTTUUMXTYSYAXOXTYSSUULXNPUBUMWFWGWHW
      IWJWKWLWNWOWPWQYIWRWSXCYRUUFFSPWRYRFUUEWTXAXBXD $.
      $( [14-Jan-2014] $)
  $}

  ${
    $d b f g h j l m o U $.  $d b f j l m n o U $.  $d b f j n o A $.
    $d b f g h j o F $.  $d b f g h j o N $.  $d b f g h j o X $.
    $d g h l m z $.  $d n o F $.  $d n o N $.  $d n o X $.
    $( Definition of an inductive closure at a successor.  (Contributed by FL,
       14-Jan-2014.) $)
    indclsbusuc $p |- ( ( ( U e. A /\ X C_ U ) /\
      A. f e. F E. n e. NN f e. ( U ^m ( U ^m ( 1 ... n ) ) ) /\ N e. NN0 ) ->
        ( ( X ( IndClsBu ` U ) F ) ` ( N + 1 ) ) =
        ( ( ( X ( IndClsBu ` U ) F ) ` N ) u.
        { o | E. f e. F E. j e. ( dom f i^i
          ( _V X. ( ( X IndClsBu F ) ` N ) ) ) o = ( f ` j ) } ) ) $=
      ( vl vm wcel wa c1 co cn0 wceq cvv a1i wi vb vg vh vz wss cv cmap cn wrex
      cfz wral w3a caddc clinclb cfv cpw cun cmpt2 cc0 cdm cmin cxp cin cab cif
      cmpt cseq0 peano2nn0 indclsbu syl3an3 pwexg adantr 3ad2ant1 mpt2exg nn0ex
      syl11anc mptexg ax-mp simp3 seq0p1g imp syl21anc wf ssexg biimprd adantld
      elpwg syl ancoms pm2.43i ad2antrr wn r19.29 oprex elmapg wfn sylbi syl6bi
      elin ex mpan2 3adant3 syl5ibr 3expd com12 rexlimiv visset rgen ab2rexexg2
      inex1 sylancl mpbird eqid sylib elpw sylibr 3ad2ant3 fveq2d xpeq2d ineq2d
      wb eleq2d anbi1d rexbidv2 abbidv cr clt wbr lt01 syl22anc 3imp fvex ax-17
      1re uneq12 eqtrd syl111anc cc ax1cn 3eqtrd df-f wfun fnfun fvelrn syl5com
      ssel com23 com3l impcom eleq1 exp3a com3r abssdv lemindclsbu adantlr dmex
      ifclda ralrimiva fmpt unss biimpi syl2anb unex rgen2a fmptb mpbii seq0clg
      crn syl23anc eqeq1 opreq1 rexbidva ifbieq2d fvmptg wne nn0re peano2re cle
      ifcl nn0ge0 addgegt0 gt0ne0 df-ne wal simpl1 fdm biimpa 3exp1 com15 com24
      ffvelrn alrimiv abss adantl iffalse eleq1d mpancom eqeltrd pm3.2i cbvmpt2
      unexg opreqd ovmpt2ga eqcomd axaddrcl nn0cn subidi opreq2d addid1 rexbidv
      addsubass uneq12d ) BALZIBUEZMZCUFZBBNEUFZUJOZUGOZUGOLZEUHUIZCGUKZHPLZULZ
      HNUMOZIGBUNUOOZUOZUYEJKBUPZUYHJUFZKUFZUQZURZUAPUAUFZUSQZIFUFZDUFZUXPUOZQZ
      DUXPUTZRUYMNVAOZIGUNOZUOZVBZVCZUIZCGUIZFVDZVEZVFZVGOZUOZHVUJUOZUYEVUIUOZU
      YLOZHUYFUOZUYRDUYSRHVUAUOZVBZVCZUIZCGUIZFVDZUQZUYCUXOUYBUYEPLZUYGVUKQHVHZ
      ABCDKEFGUYEIUAJVIVJUYDUYLRLZVUIRLZUYCVUKVUNQZUYDUYHRLZVVHVVEUXOUYBVVHUYCU
      XMVVHUXNBAVKVLVMZVVIJKUYHUYHUYKRRVNVPZVVFUYDPRLVVFVOUAPVUHRVQVRSZUXOUYBUY
      CVSZVVEVVFMZUYCVVGRRUYLVUIHVTWAWBUYDVUNVULVUMUQZVVBUYDVULUYHLZVUMUYHLZVVN
      RLZVUNVVNQUYDVVEVVFUYCPUYHVUIWCZUYHUYHVBUYHUYLWCZVVOVVJVVKVVLUYDVUHUYHLZU
      APUKVVRUYDVVTUAPUYDUYMPLZMZUYNIVUGUYHUYDIUYHLZVWAUYNUXOUYBVWCUYCUXOVWCUXN
      UXMUXOVWCTZUXNUXMMIRLZVWDIBAWDZVWEUXNVWCUXMVWEVWCUXNIBRWGWEWFWHWIWJVMWKVW
      BUYNWLZMZVUGUYHLZVUGBUEZVWHVUFFBUYDVUFUYOBLZTZVWAVWGUXOUYBVWLUYCUXOUYBVWL
      UXMUYBVWLTUXNUYBVUFUXMVWKUYBVUFUXMVWKTZUYBVUFMUYAVUEMZCGUIVWMUYAVUECGWMVW
      NVWMCGVWNUXPGLZVWMUYAVUEVWOVWMTZUXTVUEVWPTZEUHUXTVWQTUXQUHLZVUEUXTVWPUYRU
      XTVWPTZDVUDUYPVUDLZUYPUYSLZUYPVUCLZMUYRVWSTZUYPUYSVUCWSVXAVXCVXBUYRVXAVWS
      UYRVXAUXTVWOVWMUYRVXAUXTVWOULZUXMVWKVXDUXMMVWKUYRUYQBLZVXDUXMVXEVXAUXTUXM
      VXETZVWOUXTVXAVXFUXMUXTVXAVXEUXMUXSRLZUXTVXAVXETZTBUXRUGWNZUXMVXGMZUXTUXS
      BUXPWCZVXHBUXSUXPARWOZVXKUXPUXSWPZUXPUVHZBUEZMVXHUXSBUXPUUAVXMVXOVXHVXMUX
      PUUBZVXOVXHTUXSUXPUUCVXPVXAVXOVXEVXPVXAVXOVXETVXPVXAMUYQVXNLVXOVXEUYPUXPU
      UDVXNBUYQUUFUUEWTUUGWHWAWQWRXAUUHUUIXBWAUYOUYQBUUJZXCUUKXDXEVLWQXFXESXFWA
      XEXFWHWTUULVLWAXBWKUUMVWHVUGRLZVWIVWJYAVWHGRLZVUDRLZCGUKVXRUYDVXSVWAVWGUX
      OUYBVXSUYCUXMUYBVXSUXNABCEGUUNUUOXBZWKVXTCGVXTVWOUYSVUCUXPCXGUUPZXJSXHCDF
      GVUDUYQRRXIXKVUGBRWGWHXLUUQUURUAPUYHVUHVUIVUIXMZUUSXNUYDUYKUYHLZKUYHUKJUY
      HUKZVVSVYDJKUYHUYIUYHLZUYJUYHLZMUYKBUEZVYDVYFUYIBUEZUYJBUEZVYHVYGUYIBJXGZ
      XOUYJBKXGZXOVYIVYJMVYHUYIUYJBUUTUVAUVBUYKBUYIUYJVYKVYLUVCXOXPUVDVYEVVSYAU
      YDJKUYHUYHUYHUYKUYLUYLXMUVESUVFVVMUYCVVRVVSULVVORRUYHUYLVUIHUVGWAUVIUYDVU
      MUYEUSQZIUYRDUYSRUYENVAOZVUAUOZVBZVCZUIZCGUIZFVDZVEZUYHUYDVVCWUARLZVUMWUA
      QUYCUXOVVCUYBVVDXQUYDVWEVYTRLZWUBUXOUYBVWEUYCUXNUXMVWEVWFWIVMUYDVXSVYQRLZ
      CGUKWUCVYAWUDCGWUDVWOUYSVYPVYBXJSXHCDFGVYQUYQRRXIXKZVYMIVYTRUVSVPUAUYEVUH
      WUAPRVUIUYMUYEQZUYNVYMVUGVYTIUYMUYEUSUVJWUFVUFVYSFWUFVUEVYRCGWUFVUEVYRYAV
      WOWUFUYRUYRDVUDVYQWUFVWTUYPVYQLZUYRWUFVUDVYQUYPWUFVUCVYPUYSWUFVUBVYORWUFU
      YTVYNVUAUYMUYENVAUVKXRXSXTYBYCYDVLUVLYEUVMVYCUVNVPZVYMWLZUYDWUAUYHLZUYDUY
      EUSUVOZWUIUYDUYEYFLZUSUYEYGYHZWUKUYDHYFLZWULUYCUXOWUNUYBHUVPZXQZHUVQWHUYD
      WUNNYFLZUSHUVRYHZUSNYGYHZWUMWUPWUQUYDYNSUYCUXOWURUYBHUVTZXQWUSUYDYISHNUWA
      ZYJUYEUWBZVPUYEUSUWCZXNWUIUYDMWUJVYTUYHLZUYDWVDWUIUYDWVDVYTBUEZUYDVYSVWKT
      ZFUWDWVEUYDWVFFUXOUYBUYCWVFUYBUXOUYCWVFTUYBVYSUYCUXOVWKUYBVYSUYCUXOVWKTTZ
      UYBVYSMUYAVYRMZCGUIWVGUYAVYRCGWMWVHWVGCGWVHVWOWVGUYAVYRVWOWVGTZUXTVYRWVIT
      ZEUHUXTWVJTVWRVYRUXTWVIUYRUXTWVITZDVYQUYRWUGWVKUYRWUGUXTVWOWVGUYRWUGUXTVW
      OULZUYCUXOVWKWVLUYCUXOULVWKUYRVXEWVLUYCUXOVXEWUGUXTVWOUYCUXOVXETTZWUGVXAU
      YPVYPLZMUXTVWOWVMTTZUYPUYSVYPWSVXAWVOWVNUXOUXTVWOUYCVXAVXEUXMUXTVWOUYCVXH
      TTZTZUXNUXMVXGWVQVXIVXJUXTVXKWVPVXLVXKVWOUYCVXAVXEVXKVWOUYCULZVXAMVXKUYPU
      XSLZVXEVXKVWOUYCVXAUWEWVRVXAWVSWVRUYSUXSUYPVXKVWOUYSUXSQUYCUXSBUXPUWFVMYB
      UWGUXSBUYPUXPUWKVPUWHWRXAVLUWIVLWQYKYKVXQXCXDXDXEXFXESXFWAXEXFWHWTUWJXEYK
      UWLVYSFBUWMXPUYDWUCWVDWVEYAWUEVYTBRWGWHXLUWNWUIWUJWVDYAUYDWUIWUAVYTUYHVYM
      IVYTUWOZUWPVLXLUWQUWRUYDVULRLZVUMRLZMZVVQWWCUYDWWAWWBHVUJYLUYEVUIYLUWSSVU
      LVUMRRUXAWHVVOVVPVVQULZVUNVULVUMUBUCUYHUYHUBUFZUCUFZUQZURZOVVNWWDUYLWWHVU
      LVUMUYLWWHQWWDJKUBUCUDUYHUYHUYKWWGUDUFZUYKLZUBYMWWJUCYMWWIWWGLZJYMWWKKYMU
      YIWWEUYJWWFYOUWTSUXBUBUCVULVUMUYHUYHWWGVVNWWHRWWEVULWWFVUMYOWWHXMUXCYPYQU
      YDVULVUOVUMVVAUYDVUOVULABCDKEFGHIUAJVIUXDUYDVUMWUAVYTVVAWUHUYDWUIWUAVYTQU
      YDWUKWUIUYCUXOWUKUYBUYCWULWUMWUKUYCWUNWUQWULWUOYNHNUXEXKUYCWUNWUQWURWUSWU
      MWUOWUQUYCYNSWUTWUSUYCYISWVAYJWVBVPXQWVCXNWVTWHUYCUXOVYTVVAQUYBUYCVYSVUTF
      UYCVYRVUSCGUYCUYRUYRDVYQVURUYCWUGUYPVURLUYRUYCVYQVURUYPUYCVYPVUQUYSUYCVYO
      VUPRUYCVYNHVUAUYCVYNHNNVAOZUMOZHUSUMOZHUYCHYRLZNYRLZWWPVYNWWMQHUXFZWWPUYC
      YSSZWWRHNNUXKYQUYCWWLUSHUMWWLUSQUYCNYSUXGSUXHUYCWWOWWNHQWWQHUXIWHYTXRXSXT
      YBYCYDUXJYEXQYTUXLYPYT $.
      $( [14-Jan-2014] $)
  $}


  $c Grammar $.

  $( Extend class notation with the class of all grammars. $)
  cgrm $a class Grammar $.

  ${
    $d x n t p s l r a $.
    $( A grammar is a structure composed of a set of non-terminal symbols
       ` n ` , of terminal symbols ` t ` , a set of productions ` p ` and a
       distinguished element of ` n ` , the start symbol ` s ` .
       Experimental. $)
    df-grm $a |- Grammar = { x | E. n E. t E. p E. s (
         ( x = <. <. n , t >. , <. p , s >. >. /\
           n e. Fin /\
           t e. Fin ) /\
         ( ( n i^i t ) = (/) /\
             p = { <. l , r >. | ( l e. ( Kleene ` ( n u. t ) ) /\ r e. (
        Kleene ` ( n u. t ) ) /\ E. a e. NN ( l ` a ) e. n ) } /\
             s e. n ) ) } $.
  $}

  $c sym $.

  $( Extend class notation with a function returning the symbols of a
     grammar. $)
  csym $a class sym $.

  $( The symbols of a grammar ` g ` .  Experimental. $)
  df-sym $a |- sym = ( g e. Grammar |-> ( ( ( 1st o. 1st ) ` g ) u. (
    ( 2nd o. 1st ) ` g ) ) ) $.

  $c prdct $.

  $( Extend class notation with a function returning the productions of a
     grammar. $)
  cprdct $a class prdct $.

  $( The productions of a grammar. $)
  df-prodct $a |- prdct = ( 1st o. 2nd ) $.

  $c conc $.

  $( Extend class notation with an operation concatenating two sequences of
     symbols. $)
  cconc $a class conc $.

  ${
    $d x y a b $.
    $( Concatenation of two words.  Experimental. $)
    df-conc $a |- conc = ( x e. _V , y e. _V |->
      if ( x = (/) , y , if ( y = (/) , x ,
      ( x u. ( a e. ( ( ( # ` x ) + 1 ) ... ( ( # ` x )
        + ( # ` y ) ) ) |-> ( y ` ( a - ( # ` x ) ) ) ) ) ) ) ) $.
  $}

  ${
    $d x y a $.  $d y A a $.
    $( Concatenation with the empty set.  (Contributed by FL, 14-Jan-2014.) $)
    isconc1 $p |- ( A e. B -> ( (/) conc A ) = A ) $=
      ( va vx vy wcel c0 co wceq chash cfv c1 caddc cfz cmin cmpt cif ifbieq12d
      cv cvv cconc cun w3a 0ex a1i elisset eqid iftrue ax-mp eleq1d mpbird 3jca
      eqeq1 eqidd biidd fveq2 opreq1d opreq12d opreq2d fveq2d mpteq12dv uneq12d
      id fveq1 df-conc ovmpt2g syl eqtrd ) ABFZGAUAHZGGIZAAGIZGGCGJKZLMHZVMAJKZ
      MHZNHZCSZVMOHZAKZPZUBZQZQZAVIGTFZATFZWDTFZUCVJWDIVIWEWFWGWEVIUDUEABUFZVIW
      GWFWHVIWDATWDAIZVIVKWIGUGVKAWCUHUIUEZUJUKULDEGATTDSZGIZESZWMGIZWKWKCWKJKZ
      LMHZWOWMJKZMHZNHZVRWOOHZWMKZPZUBZQZQWDUAVKWMWNGGCVNVMWQMHZNHZVSWMKZPZUBZQ
      ZQTWLWLVKWMXDWMXJWKGGUMWLWMUNWLWNWNWKXCGXIWLWNUOWLVCZWLWKGXBXHXKWLCWSXAXF
      XGWLWPVNWRXENWLWOVMLMWKGJUPZUQWLWOVMWQMXLUQURWLWTVSWMWLWOVMVROXLUSUTVAVBR
      RWMAIZVKVKWMXJAWCXMVKUOXMVCXMWNVLGXIGWBWMAGUMXMGUNZXMGGXHWAXNXMCXFXGVQVTX
      MXEVPVNNXMWQVOVMMWMAJUPUSUSVSWMAVDVAVBRRDECVEVFVGWJVH $.
      $( [14-Jan-2014] $)
  $}


  ${
    $d x y A a $.
    $( Concatenation with the empty set.  (Contributed by FL, 14-Jan-2014.) $)
    isconc2 $p |- ( A e. B -> ( A conc (/) ) = A ) $=
      ( va vx vy wcel c0 co wceq chash cfv caddc cfz cmin cif cvv a1i ifbieq12d
      c1 cv cconc cmpt cun w3a elisset 0ex wa adantr simpl oprex mptexg syl jca
      wn unexg ifclda 3jca eqeq1 eqidd biidd id opreq1d opreq12d opreq2d fveq2d
      fveq2 mpteq12dv uneq12d fveq1 df-conc ovmpt2g iftrue eqcom biimpi iffalse
      eqtrd eqid ax-mp pm2.61i ) ABFZAGUAHZAGIZGGGIZAACAJKZSLHZWDGJKZLHZMHZCTZW
      DNHZGKZUBZUCZOZOZAVTAPFZGPFZWOPFZUDWAWOIVTWPWQWRABUEZWQVTUFQVTWBGWNPWQVTW
      BUGUFQVTWNPFWBUNZVTWCAWMPVTWPWCWSUHVTWCUNZUGZVTWLPFZUGWMPFXBVTXCVTXAUIXBW
      HPFZXCXDXBWEWGMUJQCWHWKPUKULUMAWLBPUOULUPUHUPUQDEAGPPDTZGIZETZXGGIZXEXECX
      EJKZSLHZXIXGJKZLHZMHZWIXINHZXGKZUBZUCZOZOWOUAWBXGXHAACWEWDXKLHZMHZWJXGKZU
      BZUCZOZOPXEAIZXFWBXGXRXGYDXEAGURYEXGUSYEXHXHXEXQAYCYEXHUTYEVAZYEXEAXPYBYF
      YECXMXOXTYAYEXJWEXLXSMYEXIWDSLXEAJVFZVBYEXIWDXKLYGVBVCYEXNWJXGYEXIWDWINYG
      VDVEVGVHRRXHWBWBXGYDGWNXHWBUTXHVAXHXHWCAYCAWMXGGGURXHAUSZXHAAYBWLYHXHCXTY
      AWHWKXHWEWEXSWGMXHWEUSXHXKWFWDLXGGJVFVDVCWJXGGVIVGVHRRDECVJVKULWOAIZVTWBY
      IWBWOGAWBGWNVLWBGAIAGVMVNVPWTWOWNAWBGWNVOWNAIZWTWCYJGVQWCAWMVLVRQVPVSQVP
      $.
      $( [14-Jan-2014] $)
  $}

  ${
    $d x y A a $.  $d y B a $.
    $( Definition of a concatenation.  (Contributed by FL, 14-Jan-2014.) $)
    isconc3 $p |- ( ( A e. C /\ B e. C /\ ( A =/= (/) /\ B =/= (/) ) )
    -> ( A conc B ) =
     ( A u. ( a e. ( ( ( # ` A ) + 1 ) ... ( ( # ` A )
        + ( # ` B ) ) ) |-> ( B ` ( a - ( # ` A ) ) ) ) ) ) $=
      ( vx vy wcel c0 co wceq chash cfv c1 caddc cfz cv cmin cif cvv ifbieq12d
      wne wa w3a cconc cun elisset 3ad2ant1 3ad2ant2 adantr wn oprex a1i mptexg
      cmpt syl wb unexb mpbid df-ne iffalse sylbi adantl 3ad2ant3 eleq1d mpbird
      ifclda 3jca eqeq1 eqidd biidd id fveq2 opreq1d opreq12d opreq2d mpteq12dv
      jca fveq2d uneq12d fveq1 uneq2d df-conc ovmpt2g eqid 3eqtrd eqtrd ) ACGZB
      CGZAHUAZBHUAZUBZUCZABUDIZAHJZBBHJZAADAKLZMNIZWPBKLZNIZOIZDPZWPQIZBLZUNZUE
      ZRZRZXEWLASGZBSGZXGSGZUCWMXGJWLXHXIXJWGWHXHWKACUFUGZWHWGXIWKBCUFUHZWLWNBX
      FSWLXIWNXLUIWLXFSGZWNUJZWLXMXESGZWLXHXDSGZUBZXOWLXHXPXKWLWTSGZXPXRWLWQWSO
      UKULDWTXCSUMUOVQXQXOUPWLAXDUQULURWLXFXESWKWGXFXEJZWHWJXSWIWJWOUJXSBHUSWOA
      XEUTVAVBVCZVDVEUIVFVGEFABSSEPZHJZFPZYCHJZYAYADYAKLZMNIZYEYCKLZNIZOIZXAYEQ
      IZYCLZUNZUEZRZRXGUDWNYCYDAADWQWPYGNIZOIZXBYCLZUNZUEZRZRSYAAJZYBWNYCYNYCYT
      YAAHVHUUAYCVIUUAYDYDYAYMAYSUUAYDVJUUAVKZUUAYAAYLYRUUBUUADYIYKYPYQUUAYFWQY
      HYOOUUAYEWPMNYAAKVLZVMUUAYEWPYGYGNUUCUUAYGVIVNVNUUAYJXBYCUUAYEWPXAQUUCVOV
      RVPVSTTYCBJZWNWNYCYTBXFUUDWNVJUUDVKUUDYDWOAYSAXEYCBHVHUUDAVIUUDYRXDAUUDDY
      PYQWTXCUUDYOWSWQOUUDYGWRWPNYCBKVLVOVOXBYCBVTVPWATTEFDWBWCUOWLXGXFXEXEWKWG
      XGXFJZWHWIUUEWJWIXNUUEAHUSWNBXFUTVAUIVCXTXEXEJWLXEWDULWEWF $.
      $( [14-Jan-2014] $)
  $}

  $c -.c $.

  $( Extend class notation with the symbol ` -. ` . $)
  cnots $a class -.c $.


  $( Definition of the symbol ` -. ` .  Experimental. $)
  df-nots $a |- -.c = { <. 1 , 1 >. } $.

  $c /\c $.

  $( Extend class notation with the symbol ` /\ ` . $)
  cands $a class /\c $.

  $( Definition of the symbol ` /\ ` .  Experimental. $)
  df-ands $a |- /\c = { <. 1 , 2 >. } $.

  $c \/c $.

  $( Extend class notation with the symbol ` \/ ` . $)
  cors $a class \/c $.

  $( Definition of the symbol ` \/ ` .  Experimental. $)
  df-ors $a |- \/c = { <. 1 , 3 >. } $.

  $c =>c $.

  $( Extend class notation with the symbol ` -> ` . $)
  cimps $a class =>c $.

  $( Definition of the symbol ` -> ` .  Experimental. $)
  df-imps $a |- =>c = { <. 1 , 4 >. } $.

  $c <=>c $.

  $( Extend class notation with the symbol ` <-> ` . $)
  cbis $a class <=>c $.

  $( Definition of the symbol ` <-> ` .  Experimental. $)
  df-bis $a |- <=>c = { <. 1 , 5 >. } $.

  $c _|_c $.

  $( Extend class notation with the symbol _|_ . $)
  cfals $a class _|_c $.

  $( Definition of the symbol _|_ .  Experimental. $)
  df-fals $a |- _|_c = { <. 1 , 6 >. } $.

  $c Pc $.

  $( Extend class notation with a function that returns a propositional
     variable. $)
  cPc $a class Pc $.

  $( Function that returns a propositional variable.  Experimental. $)
  df-propvar $a |- Pc = ( x e. ( ZZ>= ` 7 ) |-> { <. 1 , x >. } ) $.

  $c notc $.

  $( Extend class notation with a function that returns a negated
     proposition. $)
  cnotc $a class notc $.

  $( Function that returns a negated proposition.  Experimental. $)
  df-notc $a |- notc = ( x e. ( Kleene ` NN ) |-> ( -.c conc x ) ) $.

  $c andc $.

  $( Extend class notation with the class of function that returns two
     propositions joined with ` /\ ` . $)
  candc $a class andc $.

  $( Function that returns two propositions joined with ` /\ ` .
     Experimental. $)
  df-andc $a |- andc = ( x e. ( Kleene ` NN ) , y e. ( Kleene ` NN )
    |-> ( ( /\c conc x ) conc y ) ) $.

  $c ors $.

  $( Extend class notation with the function that returns two propositions
     joined with ` \/ ` . $)
  corc $a class ors $.

  $( Function that returns two propositions joined with ` \/ ` .
     Experimental. $)
  df-orc $a |- ors = ( x e. ( Kleene ` NN ) , y e. ( Kleene ` NN )
    |-> ( ( \/c conc x ) conc y ) ) $.

  $c impc $.

  $( Extend class notation with the function that returns two propositions
     joined with ` -> ` . $)
  cimpc $a class impc $.

  $( Function that returns two propositions joined with ` -> ` .
     Experimental. $)
  df-impc $a |- impc = ( x e. ( Kleene ` NN ) , y e. ( Kleene ` NN )
    |-> ( ( =>c conc x ) conc y ) ) $.

  $c bic $.

  $( Extend class notation with the function that returns two propositions
     joined with ` <-> ` . $)
  cbic $a class bic $.

  $( Function that returns two propositions joined with ` <-> ` .
     Experimental. $)
  df-bic $a |- bic = ( x e. ( ( Kleene ` NN ) ^m { 1 , 2 } )
    |-> ( ( <=>c conc ( x ` 1 ) ) conc ( x ` 2 ) ) ) $.

  $c derv $.

  $( Extend class notation with the relation "derives in one step". $)
  cderv $a class derv $.

  ${
    $d g x y u v p q $.
    $( The relation ` u ` derives ` v ` in one step in the grammar ` g ` .
       Experimental. $)
    df-derv $a |- derv = ( g e. Grammar |-> { <. x , y >. | ( x e. (
     Kleene ` g ) /\ y e. ( Kleene ` g ) /\ E. u e. ( Kleene ` g ) E. v
     e. ( Kleene ` g ) E. p e. ( Kleene ` g ) E. q e. ( Kleene ` g ) ( x =
     ( ( u ( conc ` g ) p ) ( conc ` g ) v ) /\ <. p , q >. e. (
     prdct ` g ) /\ y = ( ( u ( conc ` g ) q ) ( conc ` g ) v ) ) ) }
     ) $.
  $}

$( (End of FL's mathbox.) $)


