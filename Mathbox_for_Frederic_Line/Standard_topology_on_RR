$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Standard topology on RR
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Condition for an interval to belong to the range of ` (,) ` (Contributed
     by FL, 5-Jan-2009.) $)
  intrn $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) e. ran (,) ) $=
    ( cxr cxp cr cpw cioo wf wcel wa co crn wi ioof wfn ffn fnoprvrn 3expib syl
    ax-mp ) CCDZEFZGHZACIZBCIZJABGKGLIZMZNUCGUAOZUGUAUBGPUHUDUEUFCCABGQRST $.
    $( [5-Jan-2009] $)

  ${
    $d a b p r x y A $.
    $( Alternate definition of the standard topology of the reals.  (Morris.
       Def. 2.1.1 p. 34).  Morris calls the standard topology of the reals the
       euclidean topology.  (Contributed by FL, 26-Jan-2009.)  (The proof was
       shortened by Mario Carneiro, 30-Jan-2014.) $)
    altretop $p |- ( A e. ( topGen ` ran (,) ) <-> ( A C_ RR
              /\ A. y e. A E. a e. RR E. b e. RR
            ( y e. ( a (,) b ) /\ ( a (,) b ) C_ A ) ) ) $=
      ( vx vp vr cioo cfv wcel cr wss cv co wa wrex wceq crp anbi12d c2 crn ctg
      cabs cmin ccom cxp cres copn wral eqid tgioo eleq2i cbl cme remet remetba
      wb isopn ax-mp wi blrn bl2ioo eqeq2d rpre resubcl ax-addrcl opreq1 eleq2d
      caddc sseq1d opreq2 rcla42ev 3expia sylan2 eleq2 imbi1d syl5ibrcom sylbid
      syl11anc sseq1 rexlimivv sylbi rexlimiv cdiv clt wbr simpll simplr c0 wne
      ne0i adantr cxr ioon0 syl2an syl5ib imp ioo2bl syl111anc rehalfcl syl cc0
      rexr posdif biimpa syldan elrp sylanbrc 2re elrpii rpdivcl sylancl blelrn
      2pos mp3an1 eqeltrd rcla4ev sylancom ex impbii ralbii anbi2i bitri ) BHUA
      UBIZJBUCUDUEKKUFUGZUHIZJZBKLZAMZCMZDMZHNZJZYLBLZOZDKPCKPZABUIZOZYDYFBYEYF
      YEUJZYFUJZUKULYGYHYIEMZJZUUABLZOZEYEUMIZUAZPZABUIZOZYRYEUNJZYGUUIUQYEYSUO
      ZAEBYEYFKYEYSUPZYTURUSUUHYQYHUUGYPABUUGYPUUDYPEUUFUUAUUFJZUUAFMZGMZUUENZQ
      ZGRPFKPZUUDYPUTZUUJUUMUURUQUUKFUUAYEKGUULVAUSUUQUUSFGKRUUNKJZUUORJZOZUUQU
      UAUUNUUOUDNZUUNUUOVINZHNZQZUUSUVBUUPUVEUUAUUNUUOYEYSVBVCUVBUUSUVFYIUVEJZU
      VEBLZOZYPUTZUVAUUTUUOKJZUVJUUOVDUUTUVKOUVCKJZUVDKJZUVJUUNUUOVEUUNUUOVFUVL
      UVMUVIYPYOUVIYIUVCYKHNZJZUVNBLZOCDUVCUVDKKYJUVCQZYMUVOYNUVPUVQYLUVNYIYJUV
      CYKHVGZVHUVQYLUVNBUVRVJSYKUVDQZUVOUVGUVPUVHUVSUVNUVEYIYKUVDUVCHVKZVHUVSUV
      NUVEBUVTVJSVLVMVSVNUVFUUDUVIYPUVFUUBUVGUUCUVHUUAUVEYIVOUUAUVEBVTSVPVQVRWA
      WBWCYOUUGCDKKYJKJZYKKJZOZYOUUGUWCYOYLUUFJUUGUWCYOOZYLYJYKVINZTWDNZYKYJUDN
      ZTWDNZUUENZUUFUWDUWAUWBYJYKWEWFZYLUWIQUWAUWBYOWGZUWAUWBYOWHZUWCYOUWJYOYLW
      IWJZUWCUWJYMUWMYNYLYIWKWLUWAYJWMJYKWMJUWMUWJUQUWBYJXCYKXCYJYKWNWOWPWQZYJY
      KYEYSWRWSUWDUWFKJZUWHRJZUWIUUFJZUWCUWOYOUWCUWEKJUWOYJYKVFUWEWTXAWLUWDUWGR
      JZTRJUWPUWDUWGKJZXBUWGWEWFZUWRUWDUWBUWAUWSUWLUWKYKYJVEVSUWCYOUWJUWTUWNUWC
      UWJUWTYJYKXDXEXFUWGXGXHTXIXNXJUWGTXKXLUUJUWOUWPUWQUUKYEUWFUWHKUULXMXOVSXP
      UUDYOEYLUUFUUAYLQUUBYMUUCYNUUAYLYIVOUUAYLBVTSXQXRXSWAXTYAYBYCYC $.
      $( [30-Jan-2014] $) $( [26-Jan-2009] $)
  $}

