$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
 The complex ( CC ^ N ) and numerical ( RR ^ N ) spaces with N dimensions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +cv $. $( Addition of complex vectors. $)

  $( Extends class notation with addition of complex vectors. $)
  cplcv $a class +cv $.

  ${
    $d n u v x $.
    $( Addition of complex vectors in a space of dimension ` n ` .  (We might
       also define those vectors using the elements and generic operations of a
       field.  There are many structures that can be set on the elements of
       ( CC ^m ( 1 ... n ) ) or ( RR ^m ( 1 ... n ) ) but it is traditional to
       view them as vectors so I will give them this name.)  Experimental. $)
    df-addcv $a |- +cv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) ) ,
        v e. ( CC ^m ( 1 ... n ) ) |->
        ( x e. ( 1 ... n ) |-> ( ( u ` x ) + ( v ` x ) ) ) ) ) $.
  $}

  ${
    isaddrv.1 $e |- +w = ( +cv ` N ) $.
    ${
      $d x v u U $.  $d x v u n N $.  $d x v V $.
      $( Addition of complex vectors.  Experimental.  (Contributed by FL,
         14-Sep-2013.) $)
      isaddrv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
      V e. ( CC ^m ( 1 ... N ) ) ) -> ( U +w V ) =
      ( x e. ( 1 ... N ) |-> ( ( U ` x ) + ( V ` x ) ) ) ) $=
        ( vu vv vn cn wcel cc c1 cfz co cmap w3a cv cfv caddc cmpt cmpt2 wceq
        cplcv a1i opreq2 opreq2d eqidd mpteq12dv mpt2eq123dv df-addcv cvv wa
        oprex pm3.2i mpt2exg ax-mp fvmpt eqtrd 3ad2ant1 opreqd simp2 simp3 jca
        fveq1 opreq1d eqid mptexg ovmpt2 syl ) CJKZBLMCNOZPOZKZDVMKZQZBDEOBDGHV
        MVMAVLARZGRZSZVQHRZSZTOZUAZUBZOZAVLVQBSZVQDSZTOZUAZVPEWDBDVKVNEWDUCVOVK
        ECUDSZWDEWJUCVKFUEICGHLMIRZNOZPOZWMAWLWBUAZUBWDJUDWKCUCZGHWMWMWNVMVMWCW
        OWLVLLPWKCMNUFZUGZWQWOAWLWBVLWBWPWOWBUHUIUJAHGIUKVMULKZWRUMWDULKWRWRLVL
        PUNZWSUOGHVMVMWCULULUPUQURUSUTVAVPVNVOUMWEWIUCVPVNVOVKVNVOVBVKVNVOVCVDG
        HBDVMVMWCWIWDAVLWFWATOZUAVRBUCZAVLWBVLWTXAVLUHXAVSWFWATVQVRBVEVFUIVTDUC
        ZAVLWTVLWHVLVLUCXBVLVGUEXBWAWGWFTVQVTDVEUGUIWDVGVLULKWIULKMCNUNAVLWHULV
        HUQVIVJUS $.
        $( [14-Sep-2013] $)
    $}

    ${
      $d x U $.  $d x N $.  $d x V $.
      $( Closure of addition of complex vectors.  (Contributed by FL,
         14-Sep-2013.) $)
      claddrv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
        V e. ( CC ^m ( 1 ... N ) ) ) -> ( U +w V ) e. ( CC ^m ( 1 ... N ) ) )
          $=
        ( vx cn wcel cc c1 cfz co cmap w3a cv cfv caddc cmpt wf wa wi cvv wb
        axcnex oprex pm3.2i elmapg ax-mp ffvelrn ex sylbi 3ad2ant2 imp
        3ad2ant3 jca axaddcl syl6 eqid fmptd a1i syl mpbird isaddrv eleq1d ) BG
        HZAIJBKLZMLZHZCVGHZNZACDLZVGHFVFFOZAPZVLCPZQLZRZVGHZVJVQVFIVPSZVJFVFVOI
        VPVJVLVFHZVMIHZVNIHZTZVOIHVJVSWBVJVSTVTWAVJVSVTVHVEVSVTUAZVIVHVFIASZWCI
        UBHZVFUBHZTZVHWDUCWEWFUDJBKUEUFZIVFAUBUBUGUHWDVSVTVFIVLAUIUJUKULUMVJVSW
        AVIVEVSWAUAZVHVIVFICSZWIWGVIWJUCWHIVFCUBUBUGUHWJVSWAVFIVLCUIUJUKUNUMUOU
        JVMVNUPUQVPURUSVJWGVQVRUCWGVJWHUTIVFVPUBUBUGVAVBVJVKVPVGFABCDEVCVDVB $.
        $( [14-Sep-2013] $)
    $}
  $}

  $c 0cv $.

  $( Extends class notation with null vector. $)
  c0cv $a class 0cv $.

  ${
    $d n x $.
    $( The null vector in a space of dimension ` n ` .  Experimental. $)
    df-nullcv $a |- 0cv = ( n e. NN |-> ( x e. ( 1 ... n ) |-> 0 ) ) $.
  $}

  ${
    isnullcv.1 $e |- 0w = ( 0cv ` N ) $.
    ${
      $( The null vector. $)
      $d x n N $.
      $( The null vector in a space of dimension ` n ` .  (Contributed by FL,
         15-Sep-2013.) $)
      isnullcv $p |- ( N e. NN -> 0w = ( x e. ( 1 ... N ) |-> 0 ) ) $=
        ( vn cn wcel c1 cfz co cc0 cmpt cvv wa wceq id oprex mptexg ax-mp a1i
        jca c0cv cfv cv opreq2 eqidd mpteq12dv df-nullcv fvmptg eqtrd syl ) BFG
        ZULAHBIJZKLZMGZNZCUNOULULUOULPUOULUMMGUOHBIQAUMKMRSTUAUPCBUBUCZUNCUQOUP
        DTEBAHEUDZIJZKLUNFMUBURBOZAUSKUMKURBHIUEUTKUFUGAEUHUIUJUK $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x N $.
      $( The null vector is a complex vector.  (Contributed by FL,
         15-Sep-2013.) $)
      zernpl $p |- ( N e. NN -> 0w e. ( CC ^m ( 1 ... N ) ) ) $=
        ( vx cn wcel c1 cfz co cc0 cmpt cc cmap isnullcv wf cv 0cn a1i12 eqid
        fmptd axcnex oprex elmap sylibr eqeltrd ) AEFZBDGAHIZJKZLUGMIZDABCNUFUG
        LUHOUHUIFUFDUGJLUHUFDPUGFJLFQRUHSTLUGUHUAGAHUBUCUDUE $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x A $.  $d x n N $.
      $( Value of the complex vector at a specific coordinate.  (Contributed by
         FL, 15-Sep-2013.) $)
      valvze $p |- ( ( N e. NN /\ A e. ( 1 ... N ) ) -> ( 0w ` A ) = 0 ) $=
        ( vn vx cn wcel c1 cfz co wa cfv c0cv cc0 fveq1i cv cmpt wceq
        df-nullcv a1i fveq1d cvv oprex mptexg syl opreq2 eqidd mpteq12dv eqid
        fvmptg mpdan adantr cc simpr 0cn sylancl 3eqtrd syl5eq ) BGHZAIBJKZHZLZ
        ACMABNMZMZOACVDDPVCVEABEGFIEQZJKZORZRZMZMAFVAORZMZOVCAVDVJVCBNVINVISVCF
        ETUAUBUBVCAVJVKUTVJVKSZVBUTVKUCHZVMUTVAUCHZVNVOUTIBJUDUAFVAOUCUEUFEBVHV
        KGUCVIVFBSZFVGOVAOVFBIJUGVPOUHUIVIUJUKULUMUBVCVBOUNHVLOSUTVBUOUPFAOOVAU
        NVKFQASOUHVKUJUKUQURUS $.
        $( [15-Sep-2013] $)
    $}
  $}

  ${
    addcomv.1 $e |- +w = ( +cv ` N ) $.
    ${
      $d x A $.  $d x B $.  $d x N $.
      $( Vector addition is commutative.  (Contributed by FL, 15-Sep-2013.) $)
      addcomv $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) )
      /\ B e. ( CC ^m ( 1 ... N ) ) )
      -> ( A +w B ) = ( B +w A ) ) $=
        ( vx cn wcel cc c1 cfz co cmap w3a cv cfv caddc cmpt wa wceq wi wf
        axcnex oprex elmap ffvelrn 3adant1 3adant2 jca 3exp sylbi com12 imp
        addcom syl mpteq2dva isaddrv 3com23 3eqtr4d ) CGHZAIJCKLZMLZHZBVBHZNZFV
        AFOZAPZVFBPZQLZRFVAVHVGQLZRZABDLBADLZVEFVAVIVJVEVFVAHZSVGIHZVHIHZSZVIVJ
        TVEVMVPVCVDVMVPUAZUTVCVDVQVCVAIAUBZVDVQUAIVAAUCJCKUDZUEVDVRVQVDVAIBUBZV
        RVQUAIVABUCVSUEVTVRVMVPVTVRVMNVNVOVRVMVNVTVAIVFAUFUGVTVMVOVRVAIVFBUFUHU
        IUJUKULUKUMUGUMVGVHUNUOUPFACBDEUQUTVDVCVLVKTFBCADEUQURUS $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x y A $.  $d x y B $.  $d x y C $.  $d x y N $.
      $( Vector addition is associative.  (Contributed by FL, 15-Sep-2013.) $)
      addassv $p |- ( ( N e. NN /\ ( A e. ( CC ^m ( 1 ... N ) ) /\
        B e. ( CC ^m ( 1 ... N ) ) /\ C e. ( CC ^m ( 1 ... N ) ) ) ) ->
        ( ( A +w B ) +w C ) = ( A +w ( B +w C ) ) ) $=
        ( vx vy cn wcel cc c1 cfz co cmap w3a wa cv cfv caddc cmpt wceq
        isaddrv 3adant3r3 opreq1d simpl wf wi cvv oprex axcnex ffvelrnb mp2an
        ex anim12ii 3adant3 adantl axaddcl syl6 eqid fmptd wb pm3.2i a1i
        elmapg syl mpbird simpr3 syl111anc simpr1 3ad2ant2 3ad2ant3 jcad
        3adant3r1 opreq2d 3ad2ant1 imp axaddass simpr weq fveq2 opreq12d
        fvmptg sylancl 3eqtr4d mpteq2dva 3eqtr4rd 3eqtrd ) DIJZAKLDMNZONZJZBWKJ
        ZCWKJZPZQZABENZCENGWJGRZASZWRBSZTNZUAZCENZHWJHRZXBSZXDCSZTNZUAZABCENZEN
        ZWPWQXBCEWIWLWMWQXBUBWNGADBEFUCUDUEWPWIXBWKJZWNXCXHUBWIWOUFZWPXKWJKXBUG
        ZWPGWJXAKXBWPWRWJJZWSKJZWTKJZQZXAKJWOXNXQUHZWIWLWMXRWNWLXNXOWMXPWLXNXOW
        JUIJZKUIJZWLXNQXOUHLDMUJZUKWJKWRUIUIAULUMUNWMXNXPXSXTWMXNQXPUHYAUKWJKWR
        UIUIBULUMUNZUOUPUQWSWTURUSXBUTZVAWPXTXSQZXKXMVBYDWPXTXSUKYAVCVDZKWJXBUI
        UIVEVFVGWIWLWMWNVHHXBDCEFUCVIWPAGWJWTWRCSZTNZUAZENZHWJXDASZXDYHSZTNZUAZ
        XJXHWPWIWLYHWKJZYIYMUBXLWIWLWMWNVJWPYNWJKYHUGZWPGWJYGKYHWPXNXPYFKJZQYGK
        JWPXNXPYPWOXNXPUHZWIWMWLYQWNYBVKUQWOXNYPUHZWIWNWLYRWMWNXNYPXSXTWNXNQYPU
        HYAUKWJKWRUIUICULUMUNVLUQVMWTYFURUSYHUTZVAWPYDYNYOVBYEKWJYHUIUIVEVFVGHA
        DYHEFUCVIWPXIYHAEWIWMWNXIYHUBWLGBDCEFUCVNVOWPHWJXGYLWPXDWJJZQZYJXDBSZTN
        ZXFTNZYJUUBXFTNZTNZXGYLUUAYJKJZUUBKJZXFKJZUUDUUFUBWPYTUUGWOYTUUGUHZWIWL
        WMUUJWNWLYTUUGXSXTWLYTQUUGUHYAUKWJKXDUIUIAULUMUNVPUQVQWPYTUUHWOYTUUHUHZ
        WIWMWLUUKWNWMYTUUHXSXTWMYTQUUHUHYAUKWJKXDUIUIBULUMUNVKUQVQWPYTUUIWOYTUU
        IUHZWIWNWLUULWMWNYTUUIXSXTWNYTQUUIUHYAUKWJKXDUIUICULUMUNVLUQVQYJUUBXFVR
        VIUUAYTUUCUIJZXGUUDUBWPYTVSZYJUUBTUJYTUUMQXEUUCXFTGXDXAUUCWJUIXBGHVTZWS
        YJWTUUBTWRXDAWAWRXDBWAZWBYCWCUEWDUUAYTUUEUIJZYLUUFUBUUNUUBXFTUJYTUUQQYK
        UUEYJTGXDYGUUEWJUIYHUUOWTUUBYFXFTUUPWRXDCWAWBYSWCVOWDWEWFWGWH $.
        $( [15-Sep-2013] $)
    $}
  $}

  ${
    $d x A a $.  $d x N $.  $d x 0w $.
    addidv2.1 $e |- 0w = ( 0cv ` N ) $.
    addidv2.2 $e |- +w = ( +cv ` N ) $.
    $( The null vector is a left identity for vector addition.  (Contributed by
       FL, 15-Sep-2013.) $)
    addidv2 $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) )
      -> ( 0w +w A ) = A ) $=
      ( vx va cn wcel cc c1 cfz co cmap wa cv cfv caddc cmpt w3a wceq simpl
      zernpl adantr simpr 3jca isaddrv syl cc0 simpll jca valvze opreq1d wi wf
      cvv wb axcnex oprex pm3.2i elmapg ax-mp ffvelrn ex a1i com12 sylbi imp
      addid2 eqtrd mpteq2dva wfn ffn adantl ax-17 dffn5b biimpi eqcomd ) BIJZAK
      LBMNZONZJZPZDACNZGWAGQZDRZWFARZSNZTZAWDVTDWBJZWCUAWEWJUBWDVTWKWCVTWCUCVTW
      KWCBDEUDUEVTWCUFUGGDBACFUHUIWDWJGWAWHTZAWDGWAWIWHWDWFWAJZPZWIUJWHSNZWHWNW
      GUJWHSWNVTWMPWGUJUBWNVTWMVTWCWMUKWDWMUFULWFBDEUMUIUNWNWHKJZWOWHUBWDWMWPVT
      WCWMWPUOZWCVTWQWCWAKAUPZVTWQUOKUQJZWAUQJZPWCWRURWSWTUSLBMUTVAKWAAUQUQVBVC
      ZVTWRWQWRWQUOVTWRWMWPWAKWFAVDVEVFVGVHVGVIVIWHVJUIVKVLWDAWAVMZWLAUBWCXBVTW
      CWRXBXAWAKAVNVHVOXBAWLXBAWLUBGWAAHHQAJGVPVQVRVSUIVKVK $.
      $( [15-Sep-2013] $)

    $( The null vector is a right identity for vector addition.  (Contributed
       by FL, 15-Sep-2013.) $)
    addidrv2 $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) )
      -> ( A +w 0w ) = A ) $=
      ( cn wcel cc c1 cfz co cmap wa w3a wceq simpl simpr zernpl adantr 3jca
      addcomv syl addidv2 eqtrd ) BGHZAIJBKLMLZHZNZADCLZDACLZAUIUFUHDUGHZOUJUKP
      UIUFUHULUFUHQUFUHRUFULUHBDESTUAADBCFUBUCABCDEFUDUE $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x y A i $.  $d x y i N $.
    cnegvex2.1 $e |- +w = ( +cv ` N ) $.
    cnegvex2.2 $e |- 0w = ( 0cv ` N ) $.
    cnegvex2.3 $e |- N e. NN $.
    $( Existence of a left inverse for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    cnegvex2 $p |- ( A e. ( CC ^m ( 1 ... N ) ) ->
       E. x e. ( CC ^m ( 1 ... N ) ) ( x +w A ) = 0w ) $=
      ( vi vy cc c1 cfz co cmap wcel cv wceq wrex cfv caddc cmpt cc0 ci cmul
      wa wf axicn pm3.2i axmulcl ax-mp a1i wi cvv wb axcnex oprex elmapg
      ffvelrn ex sylbi imp jca syl eqid fmptd mpbird mulid2 opreq2d mulcli
      ax-1cn adddir mp3an12 ax-i2m1 opreq1i mul02 syl5eq eqtr3d mpteq2dva
      ax-17 hbmpt1 hbeq fveq1 fvmpt2 eqtr com12 com3l opreq1d mpteq2dvaf
      eqeq1d rcla4ev cplcv cn isaddrv 3exp ancoms opreq imbi2d c0cv isnullcv
      id eqeq12d rexbidva ) BKLCMNZONZPZAQZBDNZERZAXESIXDIQZXGTZXJBTZUANZUBZIXD
      UCUBZRZAXESZXFIXDUDUDUENZXLUENZUBZXEPZIXDXSXLUANZUBZXORZUFXQXFYAYDXFYAXDK
      XTUGZXFIXDXSKXTXFXJXDPZXSKPZXFYFUFZXRKPZXLKPZUFYGYHYIYJYIYHUDKPZYKUFYIYKY
      KUHUHUIUDUDUJUKULXFYFYJXFXDKBUGZYFYJUMKUNPZXDUNPZUFZXFYLUOYMYNUPLCMUQUIZK
      XDBUNUNURUKYLYFYJXDKXJBUSUTVAVBZVCXRXLUJVDUTXTVEZVFYAYEUOZXFYOYSYPKXDXTUN
      UNURUKULVGXFIXDYBUCYHYJYBUCRYQYJXSLXLUENZUANZYBUCYJYTXLXSUAXLVHVIYJXRLUAN
      ZXLUENZUUAUCYILKPYJUUCUUARUDUDUHUHVJVKXRLXLVLVMYJUUCUCXLUENUCUUBUCXLUEVNV
      OXLVPVQVRVRVDVSVCXPYDAXTXEXGXTRZXNYCXOUUDIXDXMYBIJJXGXTJQXGPIVTIJXDXSWAWB
      UUDYFUFXKXSXLUAUUDYFXKXSRZUUDXKXJXTTZRZYFUUEUMZXJXGXTWCXSUNPZUUGUUHUMXRXL
      UEUQYFUUIUUGUUEYFUUIUUGUUEUMZYFUUIUFUUFXSRZUUJIXDXSUNXTYRWDUUGUUKUUEUUGUU
      KUUEXKUUFXSWEUTWFVDUTWGUKVDVBWHWIWJWKVDXFXIXPAXEXFXGXEPZUFZXHXNEXODCWLTZR
      ZUUMXHXNRZUMZFUUOUUQUUMXGBUUNNZXNRZUMZUUTUUOUULXFUUSUULXFUUSCWMPZUULXFUUS
      UMUMHUVAUULXFUUSIXGCBUUNUUNVEWNWOUKVBWPULUUOUUPUUSUUMUUOXHUURXNXGBDUUNWQW
      JWRVGUKEXORZUUMECWSTZRZUVBGUVDUVBUVCXORZUVEUVDUVAUVEHICUVCUVCVEWTUKULUVDE
      UVCXOUVDXAWJVGUKULXBXCVG $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x N $.
    cnegvex2b.1 $e |- +w = ( +cv ` N ) $.
    cnegvex2b.2 $e |- 0w = ( 0cv ` N ) $.
    $( Existence of a left inverse for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    cnegvex2b $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) ) ->
       E. x e. ( CC ^m ( 1 ... N ) ) ( x +w A ) = 0w ) $=
      ( cn wcel cc c1 cfz co cmap wa cv wceq wrex cplcv cfv c0cv wi cif opreq2
      opreq2d eleq2d id fveq2d opreq syl fveq2 eqeq12d anbi12d rexbidv2
      imbi12d eqid 1nn elimel cnegvex2 dedth imp wb ax-mp eqeq12i rexbii a1i
      mpbird ) CHIZBJKCLMZNMZIZOZAPZBDMZEQZAVJRZVMBCSTZMZCUATZQZAVJRZVHVKWAVHVK
      WAUBBJKVHCKUCZLMZNMZIZVMBWBSTZMZWBUATZQZAWDRZUBCKCWBQZVKWEWAWJWKVJWDBWKVI
      WCJNCWBKLUDUEZUFWKVTWIAVJWDWKVMVJIVMWDIVTWIWKVJWDVMWLUFWKVRWGVSWHWKVQWFQV
      RWGQWKCWBSWKUGUHVMBVQWFUIUJCWBUAUKULUMUNUOABWBWFWHWFUPWHUPCKHUQURUSUTVAVP
      WAVBVLVOVTAVJVNVREVSDVQQVNVRQFVMBDVQUIVCGVDVEVFVG $.
      $( [15-Sep-2013] $)
  $}

  $c -cv $. $( Substraction of complex vectors. $)

  $( Extends class notation with substraction of complex vectors. $)
  cmcv $a class -cv $.

  ${
    $d n u v w $.
    $( Substraction of complex vectors in a space of dimension ` n ` .
       Experimental. $)
    df-subcv $a |- -cv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) ) ,
        v e. ( CC ^m ( 1 ... n ) ) |->
        ( iota_ w e. ( CC ^m ( 1 ... n ) ) ( v ( +cv ` n ) w ) = u ) ) ) $.
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x N $.  $d x +w $.
    addcanri.1 $e |- +w = ( +cv ` N ) $.
    addcanri.2 $e |- N e. NN $.
    addcanri.3 $e |- A e. ( CC ^m ( 1 ... N ) ) $.
    addcanri.4 $e |- B e. ( CC ^m ( 1 ... N ) ) $.
    addcanri.5 $e |- C e. ( CC ^m ( 1 ... N ) ) $.
    $( Cancellation law for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    addcanri $p |- ( ( A +w B ) = ( A +w C ) <-> B = C ) $=
      ( vx co wceq c0cv cfv cc c1 cfz cmap wcel cv wrex wi eqid cnegvex2
      pm3.35 ex w3a cn wa a1i simpr simpl3 simpl2 3jca 3adant3 jca addassv syl
      opreq2 3imp simpl1 eqcomd 3eqtrd 3exp ax-mp wb opreq1 eqeq12d eqcoms
      imbi2d mpbird com12 imp r19.23aiva syl6 addidv2 3eqtr3g impbii ) ABELZACE
      LZMZBCMWBDNOZBELZWCCELZBCAPQDRLSLZTZKUAZAELZWCMZKWFUBZUCZWBWDWEMZUCZKADEW
      CFWCUDZGUEWGWLWNUCHWGWLWKWNWGWLWKWGWKUFUGWJWNKWFWHWFTZWJWNWJWPWNWJWPWNUCW
      PWBWIBELZWICELZMZUCZUCZXAWJWGXAHBWFTZWGXAUCZICWFTZXBXCUCJXDXBWGXAXDXBWGUH
      ZWPWBWSXEWPWBUHZWQWHVTELZWHWAELZWRXFDUITZWPWGXBUHZUJWQXGMXFXIXJXIXFGUKZXE
      WPXJWBXEWPUJZWPWGXBXEWPULZXDXBWGWPUMZXDXBWGWPUNUOUPUQWHABDEFURUSXEWPWBXGX
      HMZWPWBXOUCZUCXEXPWPVTWAWHEUTUKUKVAXFWRXHXFXIWPWGXDUHZUJWRXHMXFXIXQXKXEWP
      XQWBXLWPWGXDXMXNXDXBWGWPVBUOUPUQWHACDEFURUSVCVDVEVEVFVFVFUKWJWNWTWPWJWMWS
      WBWMWSVGWCWIWCWIMWDWQWEWRWCWIBEVHWCWICEVHVIVJVKVKVLVMVNVOVPVFVFXBWDBMZIXI
      XBXRUCGXIXBXRBDEWCWOFVQUGVFVFXDWECMZJXIXDXSUCGXIXDXSCDEWCWOFVQUGVFVFVRBCA
      EUTVS $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x N $.
    addcanrg.1 $e |- +w = ( +cv ` N ) $.
    $( Cancellation law for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    addcanrg $p |- ( ( N e. NN /\
      ( A e. ( CC ^m ( 1 ... N ) ) /\ B e. ( CC ^m ( 1 ... N ) ) /\
        C e. ( CC ^m ( 1 ... N ) ) ) )
     -> ( ( A +w B ) = ( A +w C ) <-> B = C ) ) $=
      ( vx cn wcel cc c1 cfz co cmap w3a wa wceq wi cplcv cfv c0cv simpll
      simplr2 jca eqid addidv2 syl eqcomd cv wrex simpl simpr1 adantr
      cnegvex2b opreq eqeq1d biimpd opreq2 simprr simplr1 3jca addassv simpr
      simplr3 eqtr opreq1 eqeq12d com12 eqcoms ex com3l com4l 3syl com13 imp
      pm2.43i com3r syl6 ax-mp r19.23aiva 3eqtrd wb opreqi eqeq12i imbi1i a1i
      mpbird impbid ) DHIZAJKDLMNMZIZBWJIZCWJIZOZPZABEMZACEMZQZBCQZWOWRWSRZABDS
      TZMZACXAMZQZWSRZWOXDWSWOXDPZBDUATZBXAMZXGCXAMZCXFXHBXFWIWLPXHBQXFWIWLWIWN
      XDUBZWKWLWMWIXDUCUDBDXAXGXGUEZXAUEZUFUGUHXFXHXIQZXFWIWKPZGUIZAEMZXGQZGWJU
      JXFXMRZWOXNXDWOWIWKWIWNUKWIWKWLWMULUDUMGADEXGFXKUNXQXRGWJXOWJIZXQXRXQXSXR
      EXAQZXQXSXRRZRFXTXQXOAXAMZXGQZYAXTXQYCXTXPYBXGXOAEXAUOUPUQYCXSXRXFYCXSPZX
      MWOXDYDXMRZXDWOYEXDXOXBXAMZXOXCXAMZQZWOYERXBXCXOXAURWOYDYHXMWOYDYHXMRZWOY
      DPZYIYJYFYBBXAMZQZYJYIRYJWIXSWKWLOZPZYLYJWIYMWIWNYDUBYJXSWKWLWOYCXSUSWKWL
      WMWIYDUTWKWLWMWIYDUCVAUDYNYKYFXOABDXAXLVBUHUGYJYLYIWOYDYLYIRZYDWOYOYCXSWO
      YORWOXSYCYOWOXSYCYORZWOXSPZWIXSWKWMOZPYBCXAMZYGQZYPYQWIYRWIWNXSUBYQXSWKWM
      WOXSVCWKWLWMWIXSUTWKWLWMWIXSVDVAUDXOACDXAXLVBYHYTYCYLXMYTYCYLXMRRZRYGYFYT
      YGYFQZUUAYTUUBUUAYTUUBPYSYFQZUUAYSYGYFVEYLUUCYCXMUUCYLYCXMRZUUCYLUUDUUCYL
      PYSYKQUUDYSYFYKVEUUDYKYSYCYKYSQZXMYCUUEXMYCYKXHYSXIYBXGBXAVFYBXGCXAVFVGUQ
      VHVIUGVJVHVKUGVJVHVIVLVMVJVNVOVHVOVHUGVPVJVQUGVHVOVHVJVRVSVHVOVTVMVPXFWIW
      MPXICQXFWIWMXJWKWLWMWIXDVDUDCDXAXGXKXLUFUGWAVJWTXEWBWOWRXDWSWPXBWQXCEXAAB
      FWCEXAACFWCWDWEWFWGWSWRRWOBCAEURWFWH $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y N $.  $d x y +w $.
    negveud.1 $e |- +w = ( +cv ` N ) $.
    $( Existential uniqueness of vector negatives.  (Contributed by FL,
       15-Sep-2013.) $)
    negveud $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) )
      /\ B e. ( CC ^m ( 1 ... N ) ) )
          -> E! x e. ( CC ^m ( 1 ... N ) ) ( A +w x ) = B ) $=
      ( vy cn wcel cc c1 cfz co cmap w3a cv wceq wreu wrex wa wi wral c0cv cfv
      simp1 simp2 jca eqid cnegvex2b claddrv 3exp com23 imp 3adant2 com12
      addcomv 3adant3 addidv2 3ad2ant3 addassv eqcomd ex 3expd com34 3imp eqtr
      opreq1 syl com4l 3ad2ant2 com3l pm2.43i opreq2 eqeq1d rcla4ev r19.23aiv
      3syl simpl1 simpl2 simprl simprr 3jca addcanrg biimpd com3r eqcoms
      r19.21aivv wb reu4 a1i mpbird ) DHIZBJKDLMNMZIZCWMIZOZBAPZEMZCQZAWMRZWSAW
      MSZWSBGPZEMZCQZTZWQXBQZUAZGWMUBAWMUBZTZWPXAXHWPXAWPWLWNTXBBEMZDUCUDZQZGWM
      SWPXAUAZWPWLWNWLWNWOUEWLWNWOUFUGGBDEXKFXKUHZUIXLXMGWMXBWMIZXLWPXAXOXLWPOZ
      XBCEMZWMIZBXQEMZCQZTXAXPXRXTXOWPXRXLXOWPXRWPXOXRWLWOXOXRUAZWNWLWOYAWLXOWO
      XRWLXOWOXRXBDCEFUJUKULUMUNUOUMUNXPXTXPXCXJQZXPXTUAXOWPYBXLXOWPYBWPXOYBWLW
      NXOYBUAZWOWLWNYCWLWNXOYBBXBDEFUPUKUMUQUOUMUNXPYBXTXPYBXTUAZXPXPYDUAZXPXKC
      EMZCQZXPYEUAWPXOYGXLWLWOYGWNCDEXKXNFURUNUSXPYGYEXPXSXCCEMZQZYGYEUAXOWPYIX
      LXOWPYIWPXOYIWLWNWOXOYIUAWLWNXOWOYIWLWNXOWOYIWLWNXOWOOZYIWLYJTYHXSBXBCDEF
      UTVAVBVCVDVEUOUMUNXPYIYGYDXLXOYIYGYDUAUAWPYBXLYIYGXTYBXLYIYGXTUAZUAZYBXLT
      XCXKQZYLXCXJXKVFYMYHYFQZYLXCXKCEVGYIYNYKYIYNYKYIYNTXSYFQZYKXSYHYFVFYOYGXT
      XSYFCVFVBVHVBUOVHVHVBVIVJVKVHUOVHVLVLUOVHVLUGWSXTAXQWMWQXQQWRXSCWQXQBEVMV
      NVOVHUKVPVQVLWPXGAGWMWMXEWPWQWMIZXOTZXFWSXDWPYQXFUAUAZXDWSYRWSYRUACXCWSCX
      CQZYRWSYSYRWSYSTWRXCQZYRWRCXCVFWPYQYTXFWPYQYTXFUAZWPYQTZWLWNYPXOOZTZUUAUU
      BWLUUCWLWNWOYQVRUUBWNYPXOWLWNWOYQVSWPYPXOVTWPYPXOWAWBUGUUDYTXFBWQXBDEFWCW
      DVHVBWEVHVBUOWFUOUMVKWGUGWTXIWHWPWSXDAGWMXFWRXCCWQXBBEVMVNWIWJWK $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d w v u U $.  $d w v u n N $.  $d w v V $.  $d w +w $.
    issubcv.1 $e |- +w = ( +cv ` N ) $.
    issubcv.2 $e |- -w = ( -cv ` N ) $.
    $( Substraction of complex vectors in a space of dimension ` n ` .
       (Contributed by FL, 15-Sep-2013.) $)
    issubcv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
                       V e. ( CC ^m ( 1 ... N ) ) )
     -> ( U -w V ) =
       ( iota_ w e. ( CC ^m ( 1 ... N ) ) ( V +w w ) = U ) ) $=
      ( vu vv vn cn wcel cc c1 cfz co cmap w3a cv wceq crio cmcv cfv cplcv
      cmpt2 wi cvv wa oprex pm3.2i mpt2exg ax-mp opreq2 opreq2d fveq2 opreqd
      eqeq1d riotaeqbidv mpt2eq123dv df-subcv fvmptg ex com12 riotaex eqeq2
      riotabidv opreq1 eqid ovmpt2g 3exp com3r a1i opreq imbi2d mpbird syl
      3imp wb adantr riotabidva eqeq2d bitrd ) CLMZBNOCPQZRQZMZDWFMZSZBDFQZDATZ
      EQZBUAZAWFUBZUAZBDCUCUDZQZDWKCUEUDZQZBUAZAWFUBZUAZWDWGWHXBWDWPIJWFWFJTZWK
      WRQZITZUAZAWFUBZUFZUAZWGWHXBUGZUGZXHUHMZWDXIUGWFUHMZXMUIXLXMXMNWERUJZXNUK
      IJWFWFXGUHUHULUMWDXLXIWDXLXIKCIJNOKTZPQZRQZXQXCWKXOUEUDZQZXEUAZAXQUBZUFXH
      LUHUCXOCUAZIJXQXQYAWFWFXGYBXPWENRXOCOPUNUOZYCYBXTXFAXQWFYCYBXSXDXEYBXRWRX
      CWKXOCUEUPUQURUSUTAJIKVAVBVCVDUMXIXKWGWHBDXHQZXAUAZUGZUGZYGXIXAUHMZYGWTAW
      FVEWGWHYHYEWGWHYHYEIJBDWFWFXGXAXHXDBUAZAWFUBUHXEBUAXFYIAWFXEBXDVFVGXCDUAZ
      YIWTAWFYJXDWSBXCDWKWRVHURVGXHVIVJVKVLUMVMXIXJYFWGXIXBYEWHXIWQYDXABDWPXHVN
      URVOVOVPVQVRWOXBVSZWIEWRUAZYKGYLWOWJXAUAZXBYLWNXAWJYLWMWTAWFYLWKWFMZUIWLW
      SBYLWLWSUAYNDWKEWRVNVTURWAWBYMXBVSZYLFWPUAZYOHYPWJWQXABDFWPVNURUMVMWCUMVM
      VP $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x U $.  $d x N $.  $d x V $.
    subclcvd.1 $e |- -w = ( -cv ` N ) $.
    $( Closure law for vector substraction.  (Contributed by FL,
       15-Sep-2013.) $)
    subclcvd $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
       V e. ( CC ^m ( 1 ... N ) ) )
       -> ( U -w V ) e. ( CC ^m ( 1 ... N ) ) ) $=
      ( vx cn wcel cc c1 cfz co cmap w3a cv cplcv cfv wceq crio eqid issubcv
      wreu negveud 3exp com23 3imp wb oprex riotaclb a1i bicomd mpbird eqeltrd
      ) BGHZAIJBKLZMLZHZCUPHZNZACDLCFOBPQZLARZFUPSZUPFABCUTDUTTZEUAUSVBUPHZVAFU
      PUBZUNUQURVEUNURUQVEUNURUQVEFCABUTVCUCUDUEUFUSVEVDVEVDUGUSVAFUPIUOMUHUIUJ
      UKULUM $.
      $( [15-Sep-2013] $)
  $}

  $c -ucv $. $( Negative of a complex vector. $)

  $( Extends class notation with the negative of a complex vector. $)
  cnegcv $a class -ucv $.

  ${
    $d n u $.
    $( Negative of a complex vector.  Experimental. $)
    df-ucv $a |- -ucv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) )
      |-> ( ( 0cv ` n ) ( -cv ` n ) u ) ) ) $.
  $}

  ${
    $d u U $.  $d u n N $.
    isucv.1 $e |- ~w = ( -ucv ` N ) $.
    isucv.2 $e |- 0w = ( 0cv ` N ) $.
    isucv.3 $e |- -w = ( -cv ` N ) $.
    $( Negative of a complex vector.  (Contributed by FL, 15-Sep-2013.) $)
    isucv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) ) ->
       ( ~w ` U ) = ( 0w -w U ) ) $=
      ( vu vn cnegcv cfv wceq cn wcel cc c1 cfz co cmap wa wi c0cv cmcv cv
      cmpt cvv simpl oprex mptexg ax-mp a1i jca opreq2 opreq2d fveq2 eqidd
      opreq123d mpteq12dv df-ucv fvmptg syl fveq1d simpr eqid eqtrd opreq
      eqeq2d imbi2d mpbird opreq1 fveq1 eqeq1d ) EBKLZMZBNOZAPQBRSZTSZOZUAZAELZ
      DACSZMZUBZFVOWDVTAVNLZWBMZUBZWGVODBUCLZMZWGGWIWGVTWEWHACSZMZUBZWLWICBUDLZ
      MZWLHWNWLVTWEWHAWMSZMZUBZWQWNVTWEAIVRWHIUEZWMSZUFZLZWOVTAVNWTVTVPWTUGOZUA
      VNWTMVTVPXBVPVSUHXBVTVRUGOXBPVQTUIIVRWSUGUJUKULUMJBIPQJUEZRSZTSZXCUCLZWRX
      CUDLZSZUFWTNUGKXCBMZIXEXHVRWSXIXDVQPTXCBQRUNUOXIXFWHWRWRXGWMXCBUDUPXCBUCU
      PXIWRUQURUSIJUTVAVBVCVTVSWOUGOZUAXAWOMVTVSXJVPVSVDXJVTWHAWMUIULUMIAWSWOVR
      UGWTWRAWHWMUNWTVEVAVBVFULWNWKWPVTWNWJWOWEWHACWMVGVHVIVJUKULWIWFWKVTWIWBWJ
      WEDWHACVKVHVIVJUKULVOWCWFVTVOWAWEWBAEVNVLVMVIVJUK $.
      $( [15-Sep-2013] $)
  $}

  $c .cv $. $( Multiplication of complex vectors by a scalar. $)

  $( Extends class notation with scalar multiplication of complex vectors. $)
  csmcv $a class .cv $.

  ${
    $d n s u x $.
    $( Multiplication of complex vectors by a scalar in a space of dimension
       ` n ` .  Experimental. $)
    df-mulcv $a |- .cv = ( n e. NN |->
      ( s e. CC , u e. ( CC ^m ( 1 ... n ) ) |->
        ( x e. ( 1 ... n ) |-> ( s x. ( u ` x ) ) ) ) ) $.
  $}

  ${
    $d x u S s $.  $d x u U $.  $d x u n N s $.
    ismulcv.1 $e |- .t = ( .cv ` N ) $.
    $( Multiplication of complex vectors by a scalar in a space of dimension
       ` n ` .  (Contributed by FL, 15-Sep-2013.) $)
    ismulcv $p |- ( ( N e. NN /\ S e. CC /\ U e. ( CC ^m ( 1 ... N ) ) )
       -> ( S .t U ) = ( x e. ( 1 ... N ) |-> ( S x. ( U ` x ) ) ) ) $=
      ( vs vu vn csmcv cfv wceq cn wcel cc c1 cfz co cmap w3a cv cmul cmpt wi
      cmpt2 cvv wa simp1 axcnex oprex pm3.2i mpt2exg ax-mp a1i jca eqidd
      opreq2 opreq2d mpteq12dv mpt2eq123dv df-mulcv fvmptg syl opreqd simp2
      simp3 mptexg 3jca opreq1 adantr mpteq2dva fveq1 eqid ovmpt2g eqtrd opreq
      eqeq1d imbi2d mpbird ) EDJKZLZDMNZBONZCOPDQRZSRZNZTZBCERZAWDBAUAZCKZUBRZU
      CZLZUDZFWAWNWGBCVTRZWLLZUDZWQWAWGWOBCGHOWEAWDGUAZWIHUAZKZUBRZUCZUEZRZWLWG
      VTXCBCWGWBXCUFNZUGVTXCLWGWBXEWBWCWFUHXEWGOUFNZWEUFNZUGXEXFXGUIOWDSUJUKGHO
      WEXBUFUFULUMUNUOIDGHOOPIUAZQRZSRZAXIXAUCZUEXCMUFJXHDLZGHOXJXKOWEXBXLOUPXL
      XIWDOSXHDPQUQZURXLAXIXAWDXAXMXLXAUPUSUTAHIGVAVBVCVDWGWCWFWLUFNZTXDWLLWGWC
      WFXNWBWCWFVEWBWCWFVFXNWGWDUFNXNPDQUJAWDWKUFVGUMUNVHGHBCOWEXBWLXCAWDBWTUBR
      ZUCUFWRBLZAWDXAXOXPXAXOLWIWDNZWRBWTUBVIVJVKWSCLZAWDXOWKXRXOWKLXQXRWTWJBUB
      WIWSCVLURVJVKXCVMVNVCVOUNWAWMWPWGWAWHWOWLBCEVTVPVQVRVSUM $.
      $( [15-Sep-2013] $)
  $}

  $c /cv $. $( Division of complex vectors by a scalar. $)

  $( Extends class notation with scalar division of complex vectors. $)
  cdivcv $a class /cv $.

  ${
    $d n u v s $.
    $( Division of complex vectors by a scalar in a space of dimension ` n ` .
       Experimental. $)
    df-divcv $a |- /cv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) ) ,
        s e. CC |->
        ( iota_ v e. ( CC ^m ( 1 ... n ) ) ( s ( .cv ` n ) v ) = u ) ) ) $.
  $}

  ${
    $d v u U s $.  $d v S s $.  $d v u n N s $.  $d v .t $.
    isdivcv.1 $e |- /t = ( /cv ` N ) $.
    isdivcv.2 $e |- .t = ( .cv ` N ) $.
    $( Division of complex vectors by a scalar in a space of dimension ` n ` .
       (Contributed by FL, 15-Sep-2013.) $)
    isdivcv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\ S e. CC ) ->
      ( U /t S ) = ( iota_ v e. ( CC ^m ( 1 ... N ) ) ( S .t v ) = U ) ) $=
      ( vu vs vn cdivcv cfv wceq cn wcel cc c1 cfz co cmap w3a cv crio wi
      csmcv cmpt2 cvv wa simp1 oprex axcnex pm3.2i mpt2exg ax-mp a1i jca
      opreq2 opreq2d eqidd fveq2 opreqd eqeq1d riotaeqbidv mpt2eq123dv
      df-divcv fvmptg syl simp2 simp3 riotaex 3jca eqeq2 riotabidv opreq1 eqid
      ovmpt2g eqtrd opreq eqeq2d imbi2d mpbird ) FDLMZNZDOPZCQRDSTZUATZPZBQPZUB
      ZCBFTZBAUCZETZCNZAWGUDZNZUEZGWDWQWJCBWCTZWONZUEZWTWDEDUFMZNZWTHXBWTWJWRBW
      LXATZCNZAWGUDZNZUEZXGXBWJWRCBIJWGQJUCZWLXATZIUCZNZAWGUDZUGZTZXEWJWCXMCBWJ
      WEXMUHPZUIWCXMNWJWEXOWEWHWIUJXOWJWGUHPZQUHPZUIXOXPXQQWFUAUKULUMIJWGQXLUHU
      HUNUOUPUQKDIJQRKUCZSTZUATZQXHWLXRUFMZTZXJNZAXTUDZUGXMOUHLXRDNZIJXTQYDWGQX
      LYEXSWFQUAXRDRSURUSZYEQUTYEYCXKAXTWGYFYEYBXIXJYEYAXAXHWLXRDUFVAVBVCVDVEAI
      KJVFVGVHVBWJWHWIXEUHPZUBXNXENWJWHWIYGWEWHWIVIWEWHWIVJYGWJXDAWGVKUPVLIJCBW
      GQXLXEXMXICNZAWGUDUHXJCNXKYHAWGXJCXIVMVNXHBNZYHXDAWGYIXIXCCXHBWLXAVOVCVNX
      MVPVQVHVRUPXBWSXFWJXBWOXEWRXBWNXDAWGXBWMXCCBWLEXAVSVCVNVTWAWBUOUPWDWPWSWJ
      WDWKWRWOCBFWCVSVCWAWBUO $.
      $( [15-Sep-2013] $)
  $}

