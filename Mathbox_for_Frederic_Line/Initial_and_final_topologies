$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Initial and final topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d F x $.  $d I i x $.  $d Y o x $.  $d i o x $.
    bintop.1 $e |- I e. A $.
    $( Base of the initial topology. ` Y ` is a family of topologies indexed by
       ` I ` .  And ` F ` is a family of functions between the initial topology
       and the topologies ` ( F `` i ) ` .  The concept of initial topolgy
       consists in generating a topology that make the ` ( F `` i ) `
       continuous . $)
    bintop $p |- ( fi ` { x | E. i e. I
      E. o e. ( Y ` i ) x = ( `' ( F ` i ) " o ) } ) e. Bases $=
      ( cv cfv ccnv cima wceq wrex cab cvv wcel cfi ctb elisseti fvex abrexex
      abrexex2 fibas ax-mp ) AICIZEJKDILZMDUFGJZNZCFNAOZPQUJRJSQUICAFFBHTDAUHUG
      UFGUAUBUCUJPUDUE $.
      $( [16-Apr-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    $d A i x $.  $d F x $.  $d I i x $.  $d Y o x $.
    $( Base of the initial topology. ` Y ` is a family of topologies indexed by
       ` I ` .  And ` F ` is a family of functions between the initial topology
       and the topologies ` Y ` .  The concept of initial topolgy consists in
       generating a topology that make the ` F ` continuous . $)
    bintop3 $p |- ( ( I e. A /\ A. i e. I Y e. B )
      -> ( fi ` { x | E. i e. I
        E. o e. Y x = ( `' F " o ) } ) e. Bases ) $=
      ( wcel wral wa cv ccnv cima wceq wrex cab cvv cfi cfv ctb abrexexg
      ralimi anim2i abrexex2g fibas 3syl ) GBIZHCIZDGJZKUHALFMELNZOEHPZAQRIZDGJ
      ZKULDGPAQZRIUOSTUAIUJUNUHUIUMDGEAHUKCUBUCUDULDAGBRUEUORUFUG $.
      $( [21-May-2012] $) $( [19-Apr-2012] $)
  $}

  ${
    $d A i x $.  $d F o x y $.  $d I i x y $.  $d J o $.  $d K o x y $.
    $d X i x y $.  $d X o x y $.
    intopcoaconlem3.1 $e |- J = ( topGen ` ( fi ` { x | E. i e. I
        E. o e. K x = ( `' F " o ) } ) ) $.
    intopcoaconlem3.2 $e |- I e. A $.
    intopcoaconlem3.3 $e |- X e. B $.
    intopcoaconlem3.4 $e |- A. i e. I K e. Top $.
    intopcoaconlem3.5 $e |- A. i e. I F : X --> U. K $.
    intopcoaconlem3.6 $e |- I =/= (/) $.
    $( The underlying set of the initial topology is the domain of the mappings
       ` F ` . $)
    intopcoaconlem3 $p |- U. J = X $=
      ( vy cuni wcel cv wss wral wceq ccnv cima wrex cab cvv elisseti ctop
      rspec abrexexg syl rgen abrexex2g mp2an elsubops ax-mp c0 wne wi r19.2zb
      mpbi wf eqid topopn fimacnv eqcomd imaeq2 eqeq2d rcla4ev expcom ee11
      mprg eqeq1 rexbidv elab mpbir sselii cfi cfv ctg ctb fibas wa tg1 fiuni
      sseq2 unissb ax-17 visset weq elabf cdm fdm cnvimass sseq1 mpbiri a1i
      r19.23aiv syl5bir eqcoms sylbi mprgbir sstr2OLD mpi syl6bi mpan eleq2s
      ssunieq eqcomi ) JHRZJHSETZJUAZEHUBJXLUCATZFUDZXMUEZUCZEIUFZDGUFZAUGZHJYA
      UHSZYAHUAGUHSXSAUGUHSZDGUBZYBGBLUIYCDGDTGSZIUJSZYCYFDGNUKZEAIXQUJULUMUNZX
      SDAGUHUHUOUPUHYAHKUQURJYASJXQUCZEIUFZDGUFZYJYKDGGUSUTYJDGUBYKVAPYJDGVBVCY
      EJIRZFVDZYLISZYJYMDGOUKZYEYFYNYGIYLYLVEVFUMYMJXPYLUEZUCZYNYJVAYMYPJJYLFVG
      VHYNYQYJYIYQEYLIXMYLUCXQYPJXMYLXPVIVJVKVLUMVMVNXTYKAJJCMUIXOJUCZXSYJDGYRX
      RYIEIXOJXQVOVPVPVQVRVSXNEHXNXMYAVTWAZWBWAZHYSWCSZXMYTSZXNYBUUAGBSYDYBLYHX
      SDAGBUHUOUPZYAUHWDURUUAUUBWEXMYSRZUAZXNXMYSWFYARZUUDUCZUUEXNVAZYBUUGUUCYA
      UHWGURUUHUUDUUFUUDUUFUCUUEXMUUFUAZXNUUDUUFXMWHUUIUUFJUAZXNUUJQTZJUAZQYAQY
      AJWIUUKYASUUKXQUCZEIUFZDGUFZUULXTUUOAUUKUUOAWJQWKAQWLZXSUUNDGUUPXRUUMEIXO
      UUKXQVOVPVPWMUUNUULDGYEYMUUNUULVAZYOYMFWNZJUCUUQJYLFWOUUQJUURJUURUCUULUUK
      UURUAZUUNJUURUUKWHUUMUUSEIUUMUUSVAXMISUUMUUSXQUURUAFXMWPUUKXQUURWQWRWSWTX
      AXBUMUMWTXCXDXMUUFJXEXFXGXBURUMXHKXIUNEJHXJUPXK $.
      $( [21-May-2012] $) $( [21-Apr-2012] $)
  $}

  ${
    $d A i o' x $.  $d A i x y $.  $d B i o' $.  $d B i y $.  $d F o o' x $.
    $d F o x y $.  $d I i o' x $.  $d I i x y $.  $d K o o' x $.  $d K o x y $.
    $d X i o' x $.  $d X o o' x $.  $d X i x y $.
    intopcoaconlem3b.1 $e |- J = ( topGen ` ( fi ` { x | E. i e. I
        E. o e. K x = ( `' F " o ) } ) ) $.
    $( The underlying set of the initial topology is the domain of the mappings
       ` F ` . $)
    intopcoaconlem3b $p |-
      ( ( ( I e. A /\ X e. B ) /\ I =/= (/) /\
        A. i e. I ( K e. Top /\ F : X --> U. K ) )
           -> U. J = X ) $=
      ( bnjvom vy wcel wa c0 wne ctop cuni wf wral w3a cv ccnv cima wceq wrex
      cab cfi cfv ctg wss cvv simp1l abrexexg adantr ralimi 3ad2ant3 jca
      abrexex2g eqid elsubops 3syl wi topopn fimacnv eqcomd anim12i a1i imaeq2
      eqeq2d rcla4ev syl6 reximdva0 ancoms r19.35 sylib expcom 3imp wb simp1r
      eqeq1 rexbidv elabg syl mpbird sseldd ctb syl11anc fibas tg1 ex fiuni
      sseq2 imbi1d sstr2OLD ax-17 visset weq elabf r19.29 cdm fdm cnvimass
      sseq1 mpbiri r19.23aiv syl5bir adantl imp com12 sylbi impcom r19.21aiva
      unissb sylibr syl5com eqcoms pm2.43i syld r19.21aiv ssunieq unieqi
      syl5eq ) GBNZJCNZOZGPQZIRNZJISZFTZOZDGUAZUBZAUCZFUDZEUCZUEZUFZEIUGZDGUGZA
      UHZUIUJZUKUJZSZJHSYNJUUDNZLUCZJULZLUUDUAZUUEJUFYNUUBUUDJYNYEYTAUHUMNZDGUA
      ZOUUBUMNZUUBUUDULYNYEUUKYEYFYHYMUNZYMYGUUKYHYLUUJDGYIUUJYKEAIYRRUOUPUQURZ
      USYTDAGBUMUTZUMUUBUUDUUDVAVBVCYNJUUBNZJYRUFZEIUGZDGUGZYGYHYMUUSYHYGYMUUSV
      DZYHYGOYLUURVDZDGUGZUUTYGYHUVBYGUVADGYGDUCGNZOZYLYJINZJYPYJUEZUFZOZUURYLU
      VHVDUVDYIUVEYKUVGIYJYJVAVEYKUVFJJYJFVFVGVHVIUUQUVGEYJIYQYJUFYRUVFJYQYJYPV
      JVKVLVMVNVOYLUURDGVPVQVRVSYNYFUUPUUSVTYEYFYHYMWAUUAUUSAJCYOJUFZYTUURDGUVI
      YSUUQEIYOJYRWBWCWCWDWEWFWGYNUUHLUUDYNUUGUUDNZUUGUUCSZULZUUHYNUUCWHNZUVJUV
      LVDYNUULUVMYNYEUUKUULUUMUUNUUOWIZUUBUMWJWEUVMUVJUVLUUGUUCWKWLWEYNUVLUUHVD
      ZYNUULUUBSZUVKUFYNUVOVDZUVNUUBUMWMUVQUVKUVPUVKUVPUFZUVOUUGUVPULZUUHVDYNUV
      RUVLUVSUUHUVKUVPUUGWNWOUVSUVPJULZUUHYNUUGUVPJWPYNMUCZJULZMUUBUAUVTYNUWBMU
      UBUWAUUBNZYNUWBUWCUWAYRUFZEIUGZDGUGZYNUWBVDUUAUWFAUWAUWFAWQMWRAMWSZYTUWED
      GUWGYSUWDEIYOUWAYRWBWCWCWTYNUWFUWBYMYGUWFUWBVDYHYMUWFUWBYMUWFOYLUWEOZDGUG
      UWBYLUWEDGXAUWHUWBDGUWHUWBVDUVCYLUWEUWBYKUWEUWBVDZYIYKJFXBZUFZUWIYKUWJJJY
      JFXCVGUWKUWBUWAUWJULZUWEJUWJUWAWNUWDUWLEIUWDUWLVDYQINUWDUWLYRUWJULFYQXDUW
      AYRUWJXEXFVIXGXHWEXIXJVIXGWEWLURXKXLXMXNMUUBJXOXPXQXHXRVCXSXTYAUUFUUIOJUU
      ELJUUDYBVGWIHUUDKYCYD $.
      $( [21-May-2012] $) $( [24-Apr-2012] $)
  $}

  ${
    $d A i x $.  $d F i o o' x z $.  $d F i o o1 x $.  $d F i o t x z $.
    $d I i o' x z $.  $d I i o1 x $.  $d I i t x z $.  $d K i o o' x z $.
    $d K i o o1 x $.  $d K i o t x z $.  $d X i o' x $.
    intopcoaconb.1 $e |- J = ( topGen ` ( fi ` { x | E. i e. I
        E. o e. K x = ( `' F " o ) } ) ) $.
    intopcoaconb.2 $e |- I e. A $.
    intopcoaconb.3 $e |- X e. B $.
    intopcoaconb.4 $e |- A. i e. I ( K e. Top /\ F : X --> U. K ) $.
    intopcoaconb.5 $e |- I =/= (/) $.
    $( The initial topology is the coarsest one making the functions ` F `
       continuous .  (?) $)
    intopcoaconb $p |-
      J = |^| { t | ( t e. Top /\ A. i e. I F e. ( t Cn K ) ) } $=
      ( vz bnjvo1 bnjvom cv ccnv cima wceq wrex cab cfi cfv ctg ctop wcel ccn
      co wral wa cint cvv fvex eleq1 wel ax-17 hbre1 hbab hbfv hbeq opreq1
      eleq2d ralbid anbi12d ctb cuni wf simpl ralimi ax-mp bintop3 mp2an tgcl
      wb abrexex2g rspec abrexexg syl rgen a1i sylancr fibas eqid iscn
      syl11anc simprd weq imaeq2 eqeq2d cbvrexf rexbii abbii fveq2i simpr
      intopcoaconlem3 feq2d mpbird ra4e rcla4e mpan2 sylan2 wi wal wfn fnex
      cnvexg ffn sylancl adantl adantr imaexg eqeq1 rexbidv 19.21aiv elabgt ex
      wss elsubops sseli syl6 r19.21aiv mpbir2and pm3.2i c0 wne hbss hbim
      ancoms r19.29 eleq1a imp r19.23aivr expcom com12 abss sylibr syl6bi
      com23 r19.23aib mpan impcom sbincgt syldan eqint eqtri ) IATZGUAZFTZUBZUC
      ZFJUDZEHUDZAUEZUFUGZUHUGZBTZUIUJZGUUTJUKULZUJZEHUMZUNZBUEUOZLUUSUPUJUUSUV
      FUCUURUHUQUVEUUSUIUJZGUUSJUKULZUJZEHUMZUNBUUSUPUUTUUSUCZUVAUVGUVDUVJUUTUU
      SUIURUVKUVCUVIEHEQQUUTUUSQBUSEUTZEQUURUHQTZUHUJEUTEQUUQUFUVMUFUJEUTUUPEAQ
      UUOEHVAVBZVCVCVDUVKUVBUVHGUUTUUSJUKVEVFVGVHUVGUVJUURVIUJZUVGHCUJZJUIUJZEH
      UMZUVOMUVQKJVJZGVKZUNZEHUMZUVROUWAUVQEHUVQUVTVLVMVNZACUIEFGHJVOVPUURVQZVN
      UVIEHETHUJZUVIUUSVJZUVSGVKZUUKRTZUBZUUSUJZRJUMZUWEUVGUVQUVIUWGUWKUNVRUWEU
      VOUVGUWEUUQUPUJZUVOUVPUUOAUEUPUJZEHUMZUWLUWEUUOEAHCUPVSZMUWNUWEUWMEHUWEUV
      QUWMUVQEHUWCVTZFAJUUMUIWAWBWCZWDWEUUQUPWFWBUWDWBUWPRGUUSJUWFUVSUWFWGUVSWG
      ZWHWIUWEUWGUVTUWEUVQUVTUWAEHOVTWJUWEUWFKUVSGUWFKUCUWEACDESGHUUSJKUURUUJUU
      KSTZUBZUCZSJUDZEHUDZAUEZUFUGUHUUQUXDUFUUPUXCAUUOUXBEHUUNUXAFSQJUVMJUJZFUT
      UXESUTUUNSUTUXAFUTFSWKUUMUWTUUJUULUWSUUKWLWMWNWOWPWQWQMNUWCUWBUVTEHUMOUWA
      UVTEHUVQUVTWRVMVNPWSWDWTXAUWEUWJRJUWEUWHJUJZUWIUUQUJZUWJUWEUXFUXGUWEUXFUN
      ZUXGUWIUUMUCZFJUDZEHUDZUWEUXJUXKUXFUXJEHXBUXFUWIUWIUCZUXJUWIWGUXIUXLFUWHJ
      UXLFUTFRWKUUMUWIUWIUULUWHUUKWLWMXCXDXEUXHUWIUPUJZUUJUWIUCZUUPUXKVRXFZAXGU
      XGUXKVRUXHUUKUPUJZUXMUWEUXPUXFUXPEHUWBUXPEHUMOUWAUXPEHUVTUXPUVQGKXHZKDUJZ
      UXPUVTUXQUXRUNGUPUJUXPKDGXIGUPXJWBKUVSGXKNXLXMVMVNVTXNUUKUWHUPXOWBUXHUXOA
      UXOUXHUXNUUOUXJEHUXNUUNUXIFJUUJUWIUUMXPXQXQWDXRUUPUXKAUWIUPXSWIXAXTUUQUUS
      UWIUWLUUQUUSYAUVPUWNUWLMUWQUWOVPUPUUQUUSUUSWGYBVNYCYDYEYFWCYGUVAUVDUUQUUT
      YAZUUSUUTYAUVDUVAUXSHYHYIUVDUVAUXSXFZPUVCUXTEHUVAUXSEUVAEUTEQUUQUUTUVNUVL
      YJYKUWEUVAUVCUXSUWEUVQUVAUVCUXSXFZXFUWPUVQUVAUYAUVQUVAUNUVCUUTVJZUVSGVKZU
      UMUUTUJZFJUMZUNZUXSUVAUVQUVCUYFVRFGUUTJUYBUVSUYBWGUWRWHYLUYFUUPABUSZXFZAX
      GZUXSUYEUYIUYCUYEUYHAUUPUYEUYGUUOUYEUYGXFEHUYEUUOUYGUYEUUOUNUYDUUNUNZFJUD
      UYGUYDUUNFJYMUYJUYGFJUYDUUNUYGUUMUUTUUJYNYOYPWBYQYPYRXRXMUUPAUUTYSYTUUAXT
      WBUUBUUCUUDUUEUUQUUTUUFUUGUUHVNUUI $.
      $( [21-May-2012] $) $( [14-May-2012] $)
  $}

  ${
    $d A u v $.  $d J u v $.
    $( The subspace topology induced by the topology ` J ` on the set
       ` A ` . $)
    subsp2 $p |- ( ( J e. Top /\ A e. _V )
       -> ( subSp ` <. A , J >. ) = { u | E. v e. J u = ( v i^i A ) } ) $=
      ( ctop wcel cvv cop csubsp cfv cv cin wceq wrex cab c0 csn cif opeq2
      fveq2d rexeq abbidv eqeq12d opeq1 ineq2 eqeq2d rexbidv sn0top elimel 0ex
      subsp dedth2h ) DEFZCGFZCDHZIJZBKZAKZCLZMZADNZBOZMCUMDPQZRZHZIJZUTAVDNZBO
      ZMUNCPRZVDHZIJZUQURVILZMZAVDNZBOZMDCVCPDVDMZUPVFVBVHVPUOVEIDVDCSTVPVAVGBU
      TADVDUAUBUCCVIMZVFVKVHVOVQVEVJICVIVDUDTVQVGVNBVQUTVMAVDVQUSVLUQCVIURUEUFU
      GUBUCABVIVDDVCEUHUICPGUJUIUKUL $.
      $( [20-Sep-2010] $) $( [18-Sep-2010] $)
  $}


  ${
    $d J x y $.
    subspemp.1 $e |- J e. Top $.
    $( The subspace topology induced by the topology ` J ` on the empty set. $)
    subspemp $p |- ( subSp ` <. (/) , J >. ) = { (/) } $=
      ( vx vy cv c0 cin wceq wrex cab cop csubsp cfv csn wi wcel in0 eqtr
      mpan2 a1i r19.23aiv ctop 0opn ax-mp inidm eqcomi ineq1 eqeq2d rcla4ev
      mp2an eqeq1 rexbidv mpbiri impbii abbii 0ex subsp df-sn 3eqtr4i ) CEZDEZF
      GZHZDAIZCJUTFHZCJFAKLMFNVDVECVDVEVCVEDAVCVEOVAAPVCVBFHVEVAQUTVBFRSTUAVEVD
      FVBHZDAIZFAPZFFFGZHZVGAUBPVHBAUCUDVIFFUEUFVFVJDFAVAFHVBVIFVAFFUGUHUIUJVEV
      CVFDAUTFVBUKULUMUNUODCFABUPUQCFURUS $.
      $( [22-Dec-2008] $)
  $}

  $( The subspace topology induced by the topology ` J ` on the empty set. $)
  subspemp2 $p |- ( J e. Top -> ( subSp ` <. (/) , J >. ) = { (/) } ) $=
    ( ctop wcel c0 cop csubsp cfv csn wceq cif opeq2 fveq2d eqeq1d sn0top
    elimel subspemp dedth ) ABCZDAEZFGZDHZIDRAUAJZEZFGZUAIAUAAUBIZTUDUAUESUCFAU
    BDKLMUBAUABNOPQ $.
    $( [22-Dec-2008] $)


  ${
    $d A x y $.  $d B x $.
    $( The only subspace topology induced by the topology ` { (/) } ` . $)
    sbtpsines $p |- ( A e. B -> ( subSp ` <. A , { (/) } >. ) = { (/) } ) $=
      ( vx vy wcel c0 csn cop csubsp cfv cv cin wceq wrex cvv wb elisset ctop
      sn0top visset issubspt mp3an12 syl wa wex df-rex elsn anbi1i exbii 0ex
      ineq1 eqeq2d incom in0 eqtri eqeq2i syl6bb ceqsexv 3bitri bitr4i eqrdv )
      ABEZCAFGZHIJZVCVBCKZVDEZVEDKZALZMZDVCNZVEVCEZVBAOEZVFVJPZABQVCREVEOEVLVMS
      CTDVEAOVCUAUBUCVJVEFMZVKVJVGVCEZVIUDZDUEVGFMZVIUDZDUEVNVIDVCUFVPVRDVOVQVI
      DFUGUHUIVIVNDFUJVQVIVEFALZMVNVQVHVSVEVGFAUKULVSFVEVSAFLFFAUMAUNUOUPUQURUS
      CFUGUTUQVA $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${
    $d A a b y $.  $d A x y $.  $d J a b y $.  $d J x y $.
    stoig2b.1 $e |- J e. Top $.
    stoig2b.2 $e |- A e. B $.
    $( The underlying set of a subspace topology. $)
    stoig2b $p |- ( J e. Top ->
     U. ( subSp ` <. A , J >. ) C_ A ) $=
      ( vy va vb vx ctop wcel cv cop csubsp cfv cuni wi wal wss cin wceq wrex
      cab elisseti subsp unieqi eleq2i wa wex eluniab ax-17 df-rex eleq2 inss2
      sseli syl6bi a1dd adantl 19.23ai sylbi impcom com12 19.21aiv dfss2f
      sylibr ) CJKZFLZACMNOZPZKZVGAKZQZFRVIASVFVLFVJVFVKVJVGGLZHLZATZUAZHCUBZGU
      CZPZKZVFVKQZVIVSVGVHVRHGACDABEUDUEUFUGVTVGVMKZVQUHZGUIWAVQGVGUJWCWAGWAGUK
      VQWBWAVQVNCKZVPUHZHUIWBWAQZVPHCULWEWFHWFHUKVPWFWDVPWBVKVFVPWBVGVOKVKVMVOV
      GUMVOAVGVNAUNUOUPUQURUSUTVAUSUTUTVBVCFIVIAILZVIKFUKWGAKFUKVDVE $.
      $( [20-Sep-2010] $) $( [18-Sep-2010] $)
  $}


  ${
    $d A x y $.  $d J x y $.  $d X x $.
    subtopsin2.1 $e |- X = U. J $.
    $( The subspace topology induced by a singleton. $)
    subtopsin2 $p |- ( ( J e. Top /\ A e. X ) ->
      ( subSp ` <. { A } , J >. ) = { (/) , { A } } ) $=
      ( vx vy ctop wcel wa csn cop csubsp cfv c0 cpr cv cin wceq wrex cvv wb
      simpl visset a1i snex issubspt syl111anc wi wss snssi df-ss incom eqtr
      wo olc elpr sylibr syl ex syl5com mpan sylbi wn disjsn orc expcom sylbir
      pm2.61i r19.23aiv syl6bi eleq1 ctps cuni stoig eleq2i sylan2 istps 0opn
      adantr syl5bir eqid topopn biimpd eqcoms mpan9 jaoi com12 impbid eqrdv )
      BGHZACHZIZEAJZBKLMZNWMOZWLEPZWNHZWPWOHZWLWQWPFPZWMQZRZFBSZWRWLWJWPTHZWMTH
      ZWQXBUAWJWKUBXCWLEUCZUDXDWLAUEUDFWPWMTBUFUGXAWRFBXAWRUHZWSBHAWSHZXFXGWMWS
      UIZXFAWSUJXHWMWSQZWMRZXFWMWSUKWTXIRZXJXFWSWMULXAWTWMRZWRXKXJIXAXLWRXAXLIW
      PWMRZWRWPWTWMUMXMWPNRZXMUNZWRXMXNUOWPNWMXEUPZUQURUSWTXIWMUMUTVAVBURXGVCWT
      NRZXFWSAVDXAXQWRXAXQIXNWRWPWTNUMXNXOWRXNXMVEXPUQURVFVGVHUDVIVJWRWLWQWRXOW
      LWQUHZXPXNXRXMXNWQNWNHZWLWPNWNVKWLWMWNKVLHZXSWJWMBVMZUIZXTWKWMBVNWKAYAHYB
      CYAADVOAYAUJVBVPZXTWNGHZWMWNVMZRZIZXSWMWNVQZYDXSYFWNVRVSVBURVTXMWQWMWNHZW
      LWPWMWNVKWLXTYIYCXTYGYIYHYDYEWNHZYFYIWNYEYEWAWBYJYIUHYEWMYEWMRYJYIYEWMWNV
      KWCWDWEVBURVTWFVBWGWHWI $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}


  ${
    $d X x y z $.  $d J x y z $.  $d R x y z $.
    qusp.1 $e |- X = U. J $.
    qusp.2 $e |- Er R $.
    $( A quotient space is a topology. $)
    qusp $p |- ( J e. Top ->
      { x | ( x C_ ( X /. R ) /\ U. x e. J ) } e. Top ) $=
      ( vy vz ctop wcel cv cqs wss cuni wa cab wi wal cin wral simplr wceq wex
      simpll df-ral hba1 ax-17 eleq1a imim2i com23 imp3a a4s 19.23ad 19.21aiv
      sylbi adantl abss sylibr uniopn syl11anc uniuni syl5eqel jca exp31
      unissb syl5ibr r19.26 syl5ib ssab visset uniex sseq1 unieq eleq1d
      anbi12d elab 3imtr4g ssinss1 ad2antrr uninqs ad2antrl inopn 3expib com12
      impcom eqeltrd ex an4 expdimp elabf inex1 r19.21aiv cvv wb cpw elpw
      bicomi a1i anbi1d abbidv ssab2 uniexg qsexg pwexg 3syl ssexg syldan mpan
      istopg syl mpbird ) CIJZAKZDBLZMZXMNZCJZOZAPZIJZGKZXSMZYANZXSJZQZGRZYAHKZ
      SZXSJZHXSTZGXSTZOZXLYFYKXLYEGXLXMYAJZXRQARZYCXNMZYCNZCJZOZYBYDXLXRAYATZYR
      YNXLXOAYATZXQAYATZOYRYSXLYTUUAYRXLYOUUAYRQYTXLYOUUAYRXLYOOZUUAOZYOYQXLYOU
      UAUAUUCYGXPUBZYMOZAUCZHPZNZCYPUUCXLUUGCMZUUHCJXLYOUUAUDUUCUUFYGCJZQZHRZUU
      IUUAUULUUBUUAYMXQQZARZUULXQAYAUEUUNUUKHUUNUUEUUJAUUMAUFUUJAUGUUMUUEUUJQAU
      UMUUDYMUUJUUMYMUUDUUJXQUUDUUJQYMXPCYGUHUIUJUKULUMUNUOUPUUFHCUQURUUGCUSUTH
      AYAVAVBVCVDAYAXNVEVFUKXOXQAYAVGVHXRAYAUEVFXRAYAVIXRYRAYCYAGVJZVKXMYCUBZXO
      YOXQYQXMYCXNVLUUPXPYPCXMYCVMVNVOVPVQUNXLYAXSJZYJQZGRYKXLUURGXLYAXNMZYCCJZ
      OZYJUUQXLUVAYJXLUVAOZYIHXSUVBYGXNMZYGNZCJZOZYHXNMZYHNZCJZOZYGXSJYIXLUVAUV
      FUVJXLUUSUVCOZUUTUVEOZOZUVJUVAUVFOXLUVMUVJXLUVMOZUVGUVIUVMUVGXLUUSUVGUVCU
      VLYAYGXNVRVSUPUVNUVHYCUVDSZCUVKUVHUVOUBXLUVLDYAYGBFVTWAUVMXLUVOCJZUVLXLUV
      PQUVKXLUVLUVPXLUUTUVEUVPYCUVDCWBWCWDUPWEWFVCWGUUSUUTUVCUVEWHVHWIXRUVFAYGU
      VFAUGHVJXMYGUBZXOUVCXQUVEXMYGXNVLUVQXPUVDCXMYGVMVNVOWJXRUVJAYHUVJAUGYAYGU
      UOWKXMYHUBZXOUVGXQUVIXMYHXNVLUVRXPUVHCXMYHVMVNVOWJVQWLWGXRUVAAYAUVAAUGUUO
      XMYAUBZXOUUSXQUUTXMYAXNVLUVSXPYCCXMYAVMVNVOWJVHUNYJGXSUEURVCXLXSWMJXTYLWN
      XLXSXMXNWOZJZXQOZAPZWMXLXRUWBAXLXOUWAXQXOUWAWNXLUWAXOXMXNAVJWPWQWRWSWTUWC
      UVTMZXLUWCWMJZXQAUVTXAUWDXLUVTWMJZUWEUWDXLODWMJZXNWMJUWFXLUWGUWDXLCNWMDCI
      XBEVBUPDBXCXNWMXDXEUWCUVTWMXFXGXHWFGHWMXSXIXJXK $.
      $( [4-Jun-2007] $)
  $}

  ${
    $d A x $.  $d J u v x z $.  $d K u v x z $.
    eltpt.1 $e |- B = { z | E. u e. J E. v e. K z = ( u X. v ) } $.
    $( Condition for a set to belong to a product topology.. $)
    eltpt $p |- ( ( J e. Top /\ K e. Top ) -> ( A e. ( J tX K ) <-> E. x
       ( x C_ B /\ A = U. x ) ) ) $=
      ( ctop wcel wa ctx co cv cxp wceq wrex cab ctg cfv wss cuni wex txval
      eleq2d ctb wb txbas eltg3 syl sseq2i bicomi anbi1i exbii a1i 3bitrd ) GJK
      HJKLZEGHMNZKEBODOCOPQCHRDGRBSZTUAZKZAOZUTUBZEVCUCQZLZAUDZVCFUBZVELZAUDZUR
      USVAEBCDGHUEUFURUTUGKVBVGUHBCDGHUIAEUTUJUKVGVJUHURVFVIAVDVHVEVHVDFUTVCIUL
      UMUNUOUPUQ $.
      $( [18-Apr-2010] $)
  $}

  ${
    topgele.1 $e |- X = U. J $.
    $( The topologies over the same set have a greatest element (the discrete
       topology) and a least element (the undiscrete topology). $)
    topgele $p |- ( J e. Top -> ( { (/) , X } C_ J /\ J C_ ~P X ) ) $=
      ( ctop wcel cuni cpw wss wa c0 cpr cop ctps istps2 eltopsp bitr3i wi
      wceq wb id eqcoms eleq1d anbi2d ax-mp prssg ibi sylbi pweq eqcomi sseq2i
      biimpi anim12i expcom a1i imp31 sylbir ) ADEZUQAAFZGZHZIJAEZURAEZIZIZJBKA
      HZABGZHZIZVDURALMEUQURANAOPUQUTVCVHUTVCVHQQUQVCUTVHVCVEUTVGVCVABAEZIZVEBU
      RRZVCVJSCVKVBVIVAVKURBAURBRZURBVLTUAUBUCUDVJVEJBAAAUEUFUGUTVGUSVFAVFUSVKV
      FUSRCBURUHUDUIUJUKULUMUNUOUP $.
      $( [18-Apr-2010] $)

    $( The topologies over the same set have a greatest element (the discrete
       topology). $)
    topge $p |- ( J e. Top -> J C_ ~P X ) $=
      ( ctop wcel c0 cpr wss cpw topgele simprd ) ADEFBGAHABIHABCJK $.
      $( [1-Jan-2011] $)
  $}

  ${
    ptincpw.1 $e |- X = U. J $.
    ptincpw.2 $e |- Y = U. K $.
    $( A product topology is included in the powerset of the cross product of
       the underlying sets.  The cross product and the empty set are elements
       of a product topology. $)
    ptincpw $p |- ( ( J e. Top /\ K e. Top )
      -> ( { (/) , ( X X. Y ) } C_ ( J tX K )
           /\ ( J tX K ) C_ ~P ( X X. Y ) ) ) $=
      ( ctx co cuni cxp wceq ctop wcel wa c0 cpr wss cpw eqid txuni preq2
      eqcoms sseq1d wb pweq sseq2d anbi12d txtop topindis syl syl5bir mpcom )
      ABGHZIZCDJZKZALMBLMNZOUOPZUMQZUMUORZQZNZABUMCDUMSZEFTUPVBOUNPZUMQZUMUNRZQ
      ZNZUQUPUSVEVAVGUPURVDUMURVDKUOUNUOUNOUAUBUCVAVGUDUOUNUOUNKUTVFUMUOUNUEUFU
      BUGUQUMLMVHABUMVCUHUMUIUJUKUL $.
      $( [18-Apr-2010] $)

  $}

  ${
    $d A a x y $.  $d J a x y $.  $d X a x y $.
    ttcnlem.1 $e |- X = U. J $.
    ttcnlem.2 $e |- F = { <. x , y >. | ( x e. X /\ y = <. x , A >. ) } $.
    $( A special case of ~ txcnopab , when one function is constant.  Bourbaki
       TG I.26 prop. 4. $)
    ttcn $p |- ( ( J e. Top /\ A e. X ) -> F e. ( J Cn ( J tX J ) ) ) $=
      ( va ctop wcel wa cv cop wceq copab ctx co ccn wi eleq1 anbi2d opeq2
      eqeq2d opabbidv eleq1d imbi12d cid cres csn cxp simpl idcn adantr wf
      simpr visset fconst jctir cnconst syl21anc eqid cfv fvresi eqcomd
      fvconst2 opeq12d pm5.32i opabbii txcnopab syl32anc vtoclg anabsi7
      syl5eqel ) EJKZCFKZLZAMZFKZBMZVRCNZOZLZABPZEEEQRZSRZDVOVPWDWFKZVOIMZFKZLZ
      VSVTVRWHNZOZLZABPZWFKZTVQWGTICFWHCOZWJVQWOWGWPWIVPVOWHCFUAUBWPWNWDWFWPWMW
      CABWPWLWBVSWPWKWAVTWHCVRUCUDUBUEUFUGWJVOVOVOUHFUIZEESRZKZFWHUJZUKZWRKZWOV
      OWIULZXCXCVOWSWIEFGUMUNWJVOVOWIFWTXAUOZLXBXCXCWJWIXDVOWIUPFWHIUQZURUSWHXA
      EEFFGGUTVAABEEWEEWQXAWNFWEVBGWMVSVTVRWQVCZVRXAVCZNZOZLABVSWLXIVSWKXHVTVSV
      RXFWHXGVSXFVRFVRVDVEVSXGWHFWHVRXEVFVEVGUDVHVIVJVKVLVMHVN $.
      $( [21-Jun-2010] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.  $d L x $.
    $( If ` F ` is continous over two topologies ` J ` and ` K ` then it is
       continuous over ` ( J i^i K ) ` . $)
    intcont $p |- ( ( ( J e. Top /\ K e. Top /\ L e. Top ) /\ U. J = U. K
    /\ ( F e. ( J Cn L ) /\ F e. ( K Cn L ) ) )
    -> F e. ( ( J i^i K ) Cn L ) ) $=
      ( vx ctop wcel w3a cuni wceq ccn co wa cin wf ccnv cv cima wral wb eqid
      iscn 3adant2 adantr 3adant1 anbi12d biimp3a wi pm3.2an3 imp 3adant3
      unint2t feq2 biimpd eqcoms 3syl adantrd elin simplbi2 ral2imi a1i
      anim12d com12 an4s mpcom inttop4 3ad2ant1 simp13 syl11anc mpbird ) BFGZCF
      GZDFGZHZBIZCIZJZABDKLGZACDKLGZMZHZABCNZDKLGZWBIZDIZAOZAPEQRZWBGZEDSZMZVOW
      EAOZWGBGZEDSZMZVPWEAOZWGCGZEDSZMZMZWAWJVNVQVTWSVNVQMVRWNVSWRVNVRWNTZVQVKV
      MWTVLEABDVOWEVOUAWEUAZUBUCUDVNVSWRTZVQVLVMXBVKEACDVPWEVPUAXAUBUEUDUFUGWKW
      OWMWQWAWJUHWAWKWOMZWMWQMZMWJWAXCWFXDWIWAWKWFWOWAVKVLVQHZWDVOJWKWFUHZVNVQX
      EVTVNVQXEVKVLVQXEUHZVMVKVLXGVKVLVQUIUJUKUJUKBCULXFVOWDVOWDJWKWFVOWDWEAUMU
      NUOUPUQXDWIUHWAWMWQWIWLWPWHEDWHWLWPWGBCURUSUTUJVAVBVCVDVEWAWBFGZVMWCWJTVN
      VQXHVTVKVLXHVMBCVFUKVGVKVLVMVQVTVHEAWBDWDWEWDUAXAUBVIVJ $.
      $( [16-Apr-2012] $) $( [27-Nov-2011] $)
  $}

  $c topX $.

  $( Extend class notation with a function whose value is a product
     topology. $)
  ctopx $a class topX $.

  ${
    $d f y t x i $.
    $( Note: does the intersection gives us the coarsest topology? Yes by
       iinss and inttop3 . It works with an empty family. The topology is
       { (/) } . $)
    $( The product topology of a family ` f ` of topologies is the coarsest
       topology over the product of the underlying sets that makes the
       projections continuous.  (Bourbaki TG I.14 ex. 3) Experimental. $)
    df-prtop $a |- topX = { <. f , y >. | ( f : dom f --> Top /\
       y = |^| { t e. Top | (
       U. t = X_ x e. dom f U. ( f ` x ) /\ A. i e. dom f
         ( X_ x e. dom f U. ( f ` x ) pr i ) e. ( t Cn ( f ` i ) ) ) } ) } $.
  $}

  ${
    $d A i $.  $d F i t x $.  $d I i t x $.
    $( Lemma for ~ usptop . $)
    usptoplem $p |- ( ( F : I --> Top /\ I e. A )
                      -> ~P X_ x e. I U. ( F ` x )
                         e. { t
                            e. Top
                            | ( U. t
                                  = X_ x e. I U. ( F ` x )
                               /\ A. i
                                  e. I
                                        ( X_ x e. I U. ( F ` x )
                                          pr
                                          i )
                                     e. ( t Cn ( F ` i ) ) ) } ) $=
      ( ctop wf wcel wa cv cfv cuni cixp cpw wceq cpro co ccn wral crab cvv
      fvex uniex a1i rgen jctr adantl ixpexg distopg 3syl unipw ffvelrn
      adantlr eqid weq fveq2 unieqd prmapcp2 simpr sylancl sylan simplr
      mapdiscn syl111anc r19.21aiva jca32 unieq eqeq1d opreq1 eleq2d ralbidv
      anbi12d elrab sylibr ) FGEHZFCIZJZAFAKZELZMZNZOZGIZWCMZWBPZWBDKZQRZWCWGEL
      ZSRZIZDFTZJZJWCBKZMZWBPZWHWNWISRZIZDFTZJZBGUAIVRWDWFWLVRVQWAUBIZAFTZJZWBU
      BIZWDVQXCVPVQXBXAAFXAVSFIVTVSEUCUDUEUFZUGUHAFWACUBUIZWBUBUJUKWFVRWBULUEVR
      WKDFVRWGFIZJZWIGIZWBWIMZWHHZXDWKVPXGXIVQFGWGEUMUNXDXGXKVRAFWAXJWBUBWGWBUO
      ADUPVTWIVSWGEUQURUSVQXBXDVRXFVPVQUTXEVAVBVQXBXDXHXFVPVQXGVCXEVAWBXJUBWHWI
      XJUOVDVEVFVGWTWMBWCGWNWCPZWPWFWSWLXLWOWEWBWNWCVHVIXLWRWKDFXLWQWJWHWNWCWIS
      VJVKVLVMVNVO $.
      $( [16-Apr-202] $) $( [5-Dec-2011] $)
  $}

  ${
    $d A i $.  $d F f i t x y $.  $d I i t x $.
    $( Definition "of the product topology of a family of topologies
       ` F ` ". $)
    istopx $p |- ( ( F : I --> Top /\ I e. A )
      -> ( topX ` F ) = |^| { t e. Top | (
        U. t = X_ x e. I U. ( F ` x ) /\ A. i e. I
          ( X_ x e. I U. ( F ` x ) pr i ) e. ( t Cn ( F ` i ) ) ) } ) $=
      ( vf vy ctop wf wcel ctopx cfv cv cuni cixp wceq cpro co ccn wral wa
      crab cint cdm wi fdm cvv fex c0 wne wrex cpw ixpexg elisset adantl fvex
      uniex a1i rgen sylancl distopg syl unipw w3a ffvelrn adantlr simplr eqid
      weq fveq2 unieqd prmapcp2 sylancom 3jca ex mapdiscn syl6 r19.21aiv unieq
      eqeq1d opreq1 eleq2d ralbidv anbi12d rcla4ev syl12anc rabn0 sylibr intex
      sylib simpl df-prtop feq1 dmeq feq2d bitrd wb fveq1 adantr r19.21aiva
      ixpeq2 ixpeq1 eqtrd eqeq2d opreq1d opreq2d eleq12d raleqbidv rabbidva
      inteqd fvopab6 syl111anc feq2 eleq1 eleq1d raleqbi1dv rabbidv imbi12d
      mpbii anabsi5 ) FIEJZFCKZELMZBNZOZAFANZEMZOZPZQZYJDNZRSZYEYLEMZTSZKZDFUAZ
      UBZBIUCZUDZQZYBEUEZFQZYBYCUBZUUAUFZFIEUGUUCUUBIEJZUUBCKZUBZYDYFAUUBYIPZQZ
      UUIYLRSZYOKZDUUBUAZUBZBIUCZUDZQZUFUUEUUHEUHKUUPUHKZUUFUUQUUBICEUIUUHUUOUJ
      UKZUURUUHUUNBIULZUUSUUHUUIUMZIKZUVAOZUUIQZUUKUVAYNTSZKZDUUBUAZUUTUUHUUIUH
      KZUVBUUBUHKZYIUHKZAUUBUAZUVHUUHAUUBYIUHUHUNUUGUVIUUFUUBCUOUPUVJAUUBUVJYGU
      UBKYHYGEUQURUSUTZVAUUIUHVBVCUVDUUHUUIVDUSUUHUVFDUUBUUHYLUUBKZYNIKZUUIYNOZ
      UUKJZUVHVEZUVFUUHUVMUVQUUHUVMUBZUVNUVPUVHUUFUVMUVNUUGUUBIYLEVFVGUUHUVMUVH
      UVPUUGUVKUVHUVRAUUBYICUHUNUUFUUGUVMVHUVLVAZAUUBYIUVOUUIUHYLUUIVIADVJYHYNY
      GYLEVKVLVMVNUVSVOVPUUIUVOUHUUKYNUVOVIVQVRVSUUNUVDUVGUBBUVAIYEUVAQZUUJUVDU
      UMUVGUVTYFUVCUUIYEUVAVTWAUVTUULUVFDUUBUVTYOUVEUUKYEUVAYNTWBWCWDWEWFWGUUNB
      IWHWIUUOWJWKUUFUUGWLGNZUEZIUWAJZUUFGHEYFAUWBYGUWAMZOZPZQZUWFYLRSZYEYLUWAM
      ZTSZKZDUWBUAZUBZBIUCZUDUUPUHUHLAHBGDWMUWAEQZUWCUWBIEJUUFUWBIUWAEWNUWOUWBU
      UBIEUWAEWOZWPWQUWOUWNUUOUWOUWMUUNBIUWOUWMUUNWRYEIKUWOUWGUUJUWLUUMUWOUWFUU
      IYFUWOUWFAUWBYIPZUUIUWOUWEYIQZAUWBUAUWFUWQQUWOUWRAUWBUWOUWRYGUWBKUWOUWDYH
      YGUWAEWSVLWTXAAUWBUWEYIXBVCUWOUWBUUBQUWQUUIQUWPAUWBUUBYIXCVCXDZXEUWOUWKUU
      LDUWBUUBUWPUWOUWHUUKUWJYOUWOUWFUUIYLRUWSXFUWOUWIYNYETYLUWAEWSXGXHXIWEWTXJ
      XKXLXMUUCUUHUUDUUQUUAUUCUUFYBUUGYCUUBFIEXNUUBFCXOWEUUCUUPYTYDUUCUUOYSUUCU
      UNYRBIUUCUUJYKUUMYQUUCUUIYJYFAUUBFYIXCZXEUULYPDUUBFUUCUUKYMYOUUCUUIYJYLRU
      WTXFXPXQWEXRXKXEXSXTVCYA $.
      $( [16-Apr-202] $) $( [4-Dec-2011] $)
  $}

  ${
    $d A i t $.  $d F i t x $.  $d I i t x $.
    $( The product topology of a family ` F ` of topologies is a topology. $)
    prtoptop $p |- ( ( F : I --> Top /\ I e. A )
      -> ( topX ` F ) e. Top ) $=
      ( vt vx vi ctop wf wcel wa ctopx cfv cv cuni cixp wceq cpro co ccn wral
      crab cint istopx c0 wne wss inttop3 wrex wi csn cpr cun 0ex p0ex unipr
      uncom un0 ixp0x eqtr4i 3eqtri ral0 pm3.2i indistop unieq ixpeq1
      eqeqan12rd wb eleq2 adantr opreq1d opreq1 adantl eleq12d imbi12d
      ralbidv2 anbi12d rcla4edv ex com13 mp2 a1d wn cvv cpw ixpexg simprr fvex
      uniex a1i rgen sylancl distopg ffvelrn ad2antrl imp simplrr eqid weq
      fveq2 unieqd prmapcp2 sylancom mapdiscn syl111anc r19.21aiva unipw jctil
      eqeq1d eleq2d ralbidv mpid com12 3syl pm2.43i pm2.61i rabn0 sylibr
      ssrab2 eqeltrd ) CGBHZCAIZJZBKLDMZNZECEMZBLZNZOZPZYHFMZQRZYCYJBLZSRZIZFCT
      ZJZDGUAZUBZGEDAFBCUCYQUDUEZYQGUFYRGIYBYQUGYBYPDGUHZYSCUDPZYBYTUIUUAYTYBUD
      UDUJZUKZNZEUDYGOZPZUUEYJQRZUUCYLSRZIZFUDTZJZUUCGIZUUAYTUIUUFUUJUUDUDUUBUL
      UUBUDULZUUEUDUUBUMUNUOUDUUBUPUUMUUBUUEUUBUQEYGURUSUTUUIFVAVBUUBVCUUAUULUU
      KYTUUAUULUUKYTUIUUAYPUUKDUUCGUUAYCUUCPZJZYIUUFYOUUJUUNUUAYDUUDYHUUEYCUUCV
      DECUDYGVEZVFUUOYNUUIFCUDUUOYJCIZYJUDIZYNUUIUUAUUQUURVGUUNCUDYJVHVIUUOYKUU
      GYMUUHUUAYKUUGPUUNUUAYHUUEYJQUUPVJVIUUNYMUUHPUUAYCUUCYLSVKVLVMVNVOVPVQVRV
      SVTWAUUAWBZYBYTUUSYBJZYTUUTYHWCIZYHWDZGIZUUTYTUIYAYGWCIZECTZUVAUUTECYGAWC
      WEZUUSXTYAWFUVDECUVDYECIYFYEBWGWHWIWJZWKYHWCWLUUTUVCYTUUTUVCUVBNZYHPZYKUV
      BYLSRZIZFCTZJZYTUUTUVLUVIUUTUVKFCUUTUUQJZYLGIZYHYLNZYKHZUVAUVKUUTUUQUVOXT
      UUQUVOUIUUSYAXTUUQUVOCGYJBWMVRWNWOUUTUUQUVAUVQYAUVEUVAUVNUVFUUSXTYAUUQWPU
      VGWKZECYGUVPYHWCYJYHWQEFWRYFYLYEYJBWSWTXAXBUVRYHUVPWCYKYLUVPWQXCXDXEYHXFX
      GUUTUVCUVMYTUIUUTYPUVMDUVBGUUTYCUVBPZJZYIUVIYOUVLUVTYDUVHYHUVSYDUVHPUUTYC
      UVBVDVLXHUVSYOUVLVGUUTUVSYNUVKFCUVSYMUVJYKYCUVBYLSVKXIXJVLVPVQVRXKXLXMXNV
      RXOYPDGXPXQYPDGXRWKXS $.
      $( [16-Apr-2012] $) $( [5-Dec-2011] $)
  $}

  ${
    $d A i $.  $d F a i t x $.  $d I a i t x $.
    $( The underlying set of a product topology. $)
    usptop $p |- ( ( F : I --> Top /\ I e. A )
      -> U. ( topX ` F ) = X_ x e. I U. ( F ` x ) ) $=
      ( vt vi va ctop wf wcel wa ctopx cfv cuni cv cixp wceq cpro co ccn wral
      crab cint cpw istopx unieqd wss ssrab2 a1i usptoplem weq unieq eqeq1d
      opreq1 eleq2d ralbidv anbi12d elrab unipw eqcomi eqeq2i biimpi ad2antrl
      sylbi rgen intfmu2 syl111anc 3eqtrd ) DHCIDBJKZCLMZNEOZNZADAOCMNPZQZVMFOZ
      RSZVKVOCMZTSZJZFDUAZKZEHUBZUCZNZVMUDZNZVMVIVJWCAEBFCDUEUFVIWBHUGZWEWBJGOZ
      NZWFQZGWBUAZWDWFQWGVIWAEHUHUIAEBFCDUJWKVIWJGWBWHWBJWHHJZWIVMQZVPWHVQTSZJZ
      FDUAZKZKWJWAWQEWHHEGUKZVNWMVTWPWRVLWIVMVKWHULUMWRVSWOFDWRVRWNVPVKWHVQTUNU
      OUPUQURWMWJWLWPWMWJVMWFWIWFVMVMUSZUTVAVBVCVDVEUIGWBWEVFVGWFVMQVIWSUIVH $.
      $( [16-Apr-2012] $) $( [5-Dec-2011] $)
  $}

  ${
    $d A i o u $.  $d F i o u x $.  $d F i t u x $.  $d I i o u x $.
    $d I i t u x $.
    $( The projections are continuous.  NEW $)
    prcnt $p |- ( ( F : I --> Top /\ I e. A ) -> A. i e. I
      ( X_ x e. I U. ( F ` x ) pr i ) e. ( ( topX ` F ) Cn ( F ` i ) ) ) $=
      ( vo vu vt ctop wf wcel wa cv cfv cuni cixp cpro co ctopx ccn ccnv cima
      wral wb prtoptop adantr ffvelrn adantlr eqid iscn syl11anc cvv ixpexg
      simplr fvex uniex a1i rgen sylancl weq fveq2 unieqd prmapcp2 sylancom
      wceq usptop feq2d mpbird wi wal w3a simprl ad2antrr ra4 ad2antll com12
      ad2antlr imp 3jca jca ex cnima syl6 19.21aiv crab cint istopx eleq2d
      oprex cnvexg imaexg 3syl elintg df-ral syl6bb syl unieq eqeq1d opreq1
      ralbidv anbi12d elrab imbi1i albii 3bitrd r19.21aiv mpbir2and ) EIDJZEBKZ
      LZAEAMZDNZOZPZCMZQRZDSNZXODNZTRKZCEXJXOEKZXSXJXTLZXSXQOZXROZXPJZXPUAZFMZU
      BZXQKZFXRUCZYAXQIKZXRIKZXSYDYILUDXJYJXTBDEUEUFXHXTYKXIEIXODUGUHZFXPXQXRYB
      YCYBUIYCUIUJUKYAYDXNYCXPJZXJXTXNULKZYMXIXMULKZAEUCYNYAAEXMBULUMXHXIXTUNYO
      AEYOXKEKXLXKDUOUPUQURUSAEXMYCXNULXOXNUIACUTXLXRXKXODVAVBVCVDYAYBXNYCXPXJY
      BXNVEXTABDEVFUFVGVHYAYHFXRYAYFXRKZYHYAYPLZYHGMZIKZYROZXNVEZXPYRXRTRZKZCEU
      CZLZLZYGYRKZVIZGVJZYQUUHGYQUUFYSYKUUCVKZYPLZUUGYQUUFUUKYQUUFLZUUJYPUULYSY
      KUUCYQYSUUEVLYAYKYPUUFYLVMYQUUFUUCXTUUFUUCVIXJYPUUFXTUUCUUDXTUUCVIYSUUAUU
      CCEVNVOVPVQVRVSYAYPUUFUNVTWAYFXPYRXRWBWCWDYQYHYGHMZOZXNVEZXPUUMXRTRZKZCEU
      CZLZHIWEZWFZKZYRUUTKZUUGVIZGVJZUUIXJYHUVBUDXTYPXJXQUVAYGAHBCDEWGWHVMYQYGU
      LKZUVBUVEUDYQXPULKZYEULKUVFUVGYQXNXOQWIUQXPULWJYEYFULWKWLUVFUVBUUGGUUTUCU
      VEGYGUUTULWMUUGGUUTWNWOWPUVEUUIUDYQUVDUUHGUVCUUFUUGUUSUUEHYRIHGUTZUUOUUAU
      URUUDUVHUUNYTXNUUMYRWQWRUVHUUQUUCCEUVHUUPUUBXPUUMYRXRTWSWHWTXAXBXCXDUQXEV
      HWAXFXGWAXF $.
      $( [21-May-2012] $) $( [18-Apr-2012] $)
  $}

