$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Initial and final topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d i o t x y I $.  $d i t x y A $.  $d i o t x y X $.  $d o t x y F $.
    $d i o t x y K $.  $d i t y B $.
    intopcoacon.1 $e |- J = ( topGen ` ( fi ` { x | E. i e. I
        E. o e. K x = ( `' F " o ) } ) ) $.
    ${
      $d o J $.
      $( The underlying set of the initial topology is the domain of the
         mappings ` F ` .  (Contributed by FL, 24-Apr-2012.) $)
      intopcoaconlem3b $p |-
      ( ( ( I e. A /\ X e. B ) /\ I =/= (/) /\
        A. i e. I ( K e. Top /\ F : X --> U. K ) )
           -> U. J = X ) $=
        ( vt wcel wa cuni wceq wrex wss cvv wi c0 wne ctop wf wral cv ccnv cima
        w3a cab cfi cfv unieqi simp1l abrexexg adantr ralimi 3ad2ant3 abrexex2g
        ctg jca eqid elsubops 3syl topopn fimacnv eqcomd anim12i imaeq2 rcla4ev
        eqeq2d syl6 reximdva0 ancoms r19.35 sylib expcom 3imp wb simp1r rexbidv
        a1i eqeq1 elabg syl mpbird sseldd ctb fibas tg1 syl2anc fiuni ax-17 vex
        mpan elabf r19.29 cdm fdm cnvimass sseq1 mpbiri rexlimivw sseq2 syl5ibr
        adantl imp ex com12 sylbi impcom ralrimiva unissb sylibr syl5com imbi1d
        sstr2 eqcoms pm2.43i syl5 ralrimiv ssunieq syl5eq ) GBMZJCMZNZGUAUBZIUC
        MZJIOZFUDZNZDGUEZUIZHOAUFZFUGZEUFZUHZPZEIQZDGQZAUJZUKULZUTULZOZJHUUCKUM
        YMJUUCMZLUFZJRZLUUCUEZUUDJPYMUUAUUCJYMYDYSAUJSMZDGUEZNUUASMZUUAUUCRYMYD
        UUJYDYEYGYLUNZYLYFUUJYGYKUUIDGYHUUIYJEAIYQUCUOUPUQURZVAYSDAGBSUSZSUUAUU
        CUUCVBVCVDYMJUUAMZJYQPZEIQZDGQZYFYGYLUURYGYFYLUURTZYGYFNYKUUQTZDGQZUUSY
        FYGUVAYFUUTDGYFDUFGMNZYKYIIMZJYOYIUHZPZNZUUQYKUVFTUVBYHUVCYJUVEIYIYIVBV
        EYJUVDJJYIFVFVGVHWBUUPUVEEYIIYPYIPYQUVDJYPYIYOVIVKVJVLVMVNYKUUQDGVOVPVQ
        VRYMYEUUOUURVSYDYEYGYLVTYTUURAJCYNJPZYSUUQDGUVGYRUUPEIYNJYQWCWAWAWDWEWF
        WGYMUUGLUUCUUFUUCMZUUFUUBOZRZYMUUGUUBWHMUVHUVJUUAWIUUFUUBWJWOYMUVJUUGTZ
        YMUUKUUAOZUVIPYMUVKTZYMYDUUJUUKUULUUMUUNWKUUASWLUVMUVIUVLYMUVKUVIUVLPZU
        UFUVLRZUUGTYMUVLJRZUVOUUGYMUUGLUUAUEUVPYMUUGLUUAUUFUUAMZYMUUGUVQUUFYQPZ
        EIQZDGQZYMUUGTYTUVTAUUFUVTAWMLWNYNUUFPZYSUVSDGUWAYRUVREIYNUUFYQWCWAWAWP
        YMUVTUUGYLYFUVTUUGTYGYLUVTUUGYLUVTNYKUVSNZDGQUUGYKUVSDGWQUWBUUGDGYKUVSU
        UGYJUVSUUGTZYHYJJFWRZPZUWCYJUWDJJYIFWSVGUVSUUGUWEUUFUWDRZUVRUWFEIUVRUWF
        YQUWDRFYPWTUUFYQUWDXAXBXCJUWDUUFXDXEWEXFXGXCWEXHURXIXJXKXLLUUAJXMXNUUFU
        VLJXQXOUVNUVJUVOUUGUVIUVLUUFXDXPXEXRVDXSXTYAUUEUUHNJUUDLJUUCYBVGWKYC $.
        $( [25-Nov-2013] $) $( [24-Apr-2012] $)
    $}

    intopcoacon.2 $e |- I e. A $.
    intopcoacon.3 $e |- X e. B $.
    intopcoacon.4 $e |- A. i e. I ( K e. Top /\ F : X --> U. K ) $.
    intopcoacon.5 $e |- I =/= (/) $.
    ${
      $d o J $.
      $( The underlying set of the initial topology is the domain of the
         mappings ` F ` .  (Contributed by FL, 21-Apr-2012.) $)
      intopcoaconlem3 $p |- U. J = X $=
        ( wcel wss wceq wrex cvv vy cuni cv wral ccnv cima cab elexi ctop wf wa
        rspec simpld abrexexg syl rgen abrexex2g mp2an elsubops ax-mp c0 wne wi
        r19.2zb mpbi simprd eqid topopn fimacnv eqcomd imaeq2 rcla4ev sylc mprg
        eqeq2d expcom eqeq1 rexbidv elab mpbir sselii cfi cfv ctg ctb fibas tg1
        fiuni sseq2 unissb ax-17 vex elabf cdm cnvimass sseq1 rexlimivw syl5ibr
        fdm mpbiri eqcoms rexlimiv sylbi mprgbir sstr2 syl6bi mpsyl mpan eleq2s
        mpi ssunieq eqcomi ) JHUBZJHPEUCZJQZEHUDJXMRAUCZFUEZXNUFZRZEISZDGSZAUGZ
        HJYBTPZYBHQGTPXTAUGTPZDGUDZYCGBLUHYDDGDUCGPZIUIPZYDYFYGJIUBZFUJZYGYIUKD
        GNULZUMZEAIXRUIUNUOUPZXTDAGTTUQURTYBHKUSUTJYBPJXRRZEISZDGSZYNYODGGVAVBY
        NDGUDYOVCOYNDGVDVEYFYIYHIPZYNYFYGYIYJVFZYFYGYPYKIYHYHVGVHUOYIJXQYHUFZRZ
        YPYNVCYIYRJJYHFVIVJYPYSYNYMYSEYHIXNYHRXRYRJXNYHXQVKVOVLVPUOVMVNYAYOAJJC
        MUHXPJRZXTYNDGYTXSYMEIXPJXRVQVRVRVSVTWAXOEHXOXNYBWBWCZWDWCZHUUAWEPZXNUU
        BPZXOYBWFYBUBZUUAUBZRZUUCUUDUKXNUUFQZXOYCUUGGBPYEYCLYLXTDAGBTUQURYBTWHU
        TXNUUAWGUUHXOVCUUFUUEUUFUUERUUHXNUUEQZXOUUFUUEXNWIUUIUUEJQZXOUUJUAUCZJQ
        ZUAYBUAYBJWJUUKYBPUUKXRRZEISZDGSZUULYAUUOAUUKUUOAWKUAWLXPUUKRZXTUUNDGUU
        PXSUUMEIXPUUKXRVQVRVRWMUUNUULDGYFYIUUNUULVCZYQYIFWNZJRUUQJYHFWSUUQJUURU
        UNUULJUURRUUKUURQZUUMUUSEIUUMUUSXRUURQFXNWOUUKXRUURWPWTWQJUURUUKWIWRXAU
        OUOXBXCXDXNUUEJXEXJXFXAXGXHKXIUPEJHXKURXL $.
        $( [25-Nov-2013] $) $( [21-Apr-2012] $)
    $}

    $( The initial topology is the coarsest one making the functions ` F `
       continuous .  (Contributed by FL, 14-May-2012.) $)
    intopcoaconb $p |- J = |^| { t e. Top | A. i e. I F e. ( t Cn K ) } $=
      ( vy wcel wa cvv cv ccnv cima wceq wrex cab cfi cfv ctg ctop co wral cint
      ccn crab fvex eleq1 ax-17 hbab hbfv hbeq opreq1 eleq2d ralbid anbi12d ctb
      hbre1 fibas tgcl ax-mp cuni wf simpl ralimi rspec eqid iscn simprd imaeq2
      sylancr eqeq2d cbvrexf rexbii abbii fveq2i intopcoaconlem3 a1i mpbird wex
      wb feq2d wi wal w3a ra42e biimpi reximi syl 3expia alrimiv wfn ffn cnvexg
      sylancl adantl imaexg alexeqd bicomd adantr clelab sylibr ex wss abrexexg
      fnex rgen abrexex2g mp2an elsubops sseli ralrimiv mpbir2and pm3.2i r19.29
      syl6 eleq1a rexlimivw syl6bi com4l imp3a com3r abss sbincgt syldan df-rab
      imp eqint inteqi 3eqtr4i ) AUAZGUBZFUAZUCZUDZFJUEZEHUEZAUFZUGUHZUIUHZBUAZ
      UJRZGUUJJUNUKZRZEHULZSZBUFZUMZIUUNBUJUOZUMUUITRUUIUUQUDUUHUIUPUUOUUIUJRZG
      UUIJUNUKZRZEHULZSBUUITUUJUUIUDZUUKUUSUUNUVBUUJUUIUJUQUVCUUMUVAEHEQQUUJUUI
      QUAZUUJREUREQUUHUIUVDUIREUREQUUGUGUVDUGREURUUFEAQUUEEHVGUSUTUTVAUVCUULUUT
      GUUJUUIJUNVBVCVDVEUUSUVBUUHVFRUUSUUGVHUUHVIVJZUVAEHEUAHRZUVAUUIVKZJVKZGVL
      ZUUAUUJUCZUUIRZBJULZUVFUUSJUJRZUVAUVIUVLSWJUVEUVMEHUVMKUVHGVLZSZEHULZUVME
      HULOUVOUVMEHUVMUVNVMVNVJVOZBGUUIJUVGUVHUVGVPUVHVPZVQVTUVFUVIUVNUVFUVMUVNU
      VOEHOVOVRUVFUVGKUVHGUVGKUDUVFACDEBGHUUIJKUUHYTUVJUDZBJUEZEHUEZAUFZUGUHUIU
      UGUWBUGUUFUWAAUUEUVTEHUUDUVSFBQJUVDJRZFURZUWCBURZUUDBURZUVSFURZUUBUUJUDUU
      CUVJYTUUBUUJUUAVSWAWBWCWDWEWEMNOPWFWGWKWHUVFUVKBJUVFUUJJRZUVJUUGRZUVKUVFU
      WHUWIUVFUWHSZUVSUUFSAWIZUWIUWJUWKUVSUUFWLZAWMZUWJUWLAUVFUWHUVSUUFUVFUWHUV
      SWNUWAUUFUVSEBHJWOUVTUUEEHUVTUUEUVSUUDBFQJUWEUWDUWGUWFUUJUUBUDUVJUUCYTUUJ
      UUBUUAVSWAWBWPWQWRWSWTUVFUWKUWMWJZUWHUVFUVJTRZUWNUVFUUATRZUWOUWPEHUVPUWPE
      HULOUVOUWPEHUVNUWPUVMUVNGKXAZKDRZUWPKUVHGXBNUWQUWRSGTRUWPKDGXOGTXCWRXDXEV
      NVJVOUUAUUJTXFWRUWOUWMUWKUUFAUVJTXGXHWRXIWHUUFAUVJXJXKXLUUGUUIUVJUUGTRZUU
      GUUIXMHCRUUEAUFTRZEHULUWSMUWTEHUVFUVMUWTUVQFAJUUCUJXNWRXPUUEEAHCTXQXRTUUG
      UUIUUIVPXSVJXTYEYAYBXPYCUUKUUNUUGUUJXMZUUIUUJXMUUOUUFYTUUJRZWLZAWMUXAUUOU
      XCAUUKUUNUXCUUNUUFUUKUXBUUNUUFUUKUXBWLZUUNUUFSUVPUUMUUESZEHUEZUXDOUUMUUEE
      HYDUVPUXFSUVOUXESZEHUEUXDUVOUXEEHYDUXGUXDEHUVOUXEUXDUVMUXEUXDWLUVNUVMUUMU
      UEUXDUUKUVMUUMUUEUXBUUKUVMUUMUUEUXBWLZWLUUKUVMSUUMUUJVKZUVHGVLZUUCUUJRZFJ
      ULZSUXHFGUUJJUXIUVHUXIVPUVRVQUXLUXHUXJUXLUUEUXBUXLUUESUXKUUDSZFJUEUXBUXKU
      UDFJYDUXMUXBFJUXKUUDUXBUUCUUJYTYFYPYGWRXLXEYHXLYIYJXIYPYGWRVTXLYKYPWTUUFA
      UUJYLXKUUGUUJYMYNYQVJLUURUUPUUNBUJYOYRYS $.
      $( [26-Nov-2013] $) $( [14-May-2012] $)

    $d o J $.
    $( The initial topology makes the functions ` F ` continuous.  (Contributed
       by FL, 15-Oct-2012.)  (Revised by Mario Carneiro, 25-Nov-2013.) $)
    intopcoaconc $p |- A. i e. I F e. ( J Cn K ) $=
      ( vy wcel ctop wa cvv ccn co cv cuni ccnv cima wral wceq wrex cab cfi cfv
      wf wb ctg fibas tgcl ax-mp eqeltri simpl ralimi rspec c0 intopcoaconlem3b
      ctb wne pm3.2i mp3an eqcomi eqid iscn sylancr simprd crab wi ra4 ad2antrr
      cint com12 simprl simprr simplr syl31anc expr syld impr ralrimiva wfn ffn
      cnima adantl fnex sylancl cnvexg syl imaexg elintrabg adantr intopcoaconb
      3syl mpbird syl6eleqr mpbir2and rgen ) FHIUAUBQZDGDUCGQZXEJIUDZFUMZFUEZEU
      CZUFZHQZEIUGZXFHRQIRQZXEXHXMSUNHAUCXKUHEIUIDGUIAUJZUKULZUOULZRKXPVEQXQRQX
      OUPXPUQURUSXNDGXNXHSZDGUGZXNDGUGNXRXNDGXNXHUTVAURVBZEFHIJXGHUDZJGBQZJCQZS
      GVCVFXSYAJUHYBYCLMVGONABCDEFGHIJKVDVHVIXGVJVKVLXFXNXHXRDGNVBZVMXFXLEIXFXJ
      IQZSZXKFPUCZIUAUBQZDGUGZPRVNVRZHYFXKYJQZYIXKYGQZVOZPRUGZYFYMPRYFYGRQZYIYL
      YFYOYIYLYFYOSYIYHYLXFYIYHVOYEYOYIXFYHYHDGVPVSVQYFYOYHYLYFYOYHSZSYOXNYHYEY
      LYFYOYHVTXFXNYEYPXTVQYFYOYHWAXFYEYPWBXJFYGIWJWCWDWEWFWDWGXFYKYNUNZYEXFXRX
      KTQZYQYDXRXITQZYRXRFTQZYSXRFJWHZYCYTXHUUAXNJXGFWIWKMJCFWLWMFTWNWOXIXJTWPW
      OYIPXKRTWQWTWRXAAPBCDEFGHIJKLMNOWSXBWGXCXD $.
      $( [26-Nov-2013] $) $( [15-Oct-2012] $)
  $}

  ${
    $d X x y z $.  $d J x y z $.  $d R x y z $.
    qusp.1 $e |- X = U. J $.
    qusp.2 $e |- Er R $.
    $( A quotient space is a topology.  (Contributed by FL, 4-Jun-2007.) $)
    qusp $p |- ( J e. Top ->
      { x | ( x C_ ( X /. R ) /\ U. x e. J ) } e. Top ) $=
      ( vy vz wcel wss cuni wa wi wal wral wceq ax-17 adantl sseq1 cvv ctop cqs
      cab cin df-ral r19.26 unissb simplr wex uniuni simpll eleq1a imim2i com23
      cv hba1 imp3a a4s exlimd alrimiv sylbi sylibr uniopn syl2anc syl5eqel jca
      abss exp31 syl5bir syl5bi ssab vex uniex unieq anbi12d elab 3imtr4g elabf
      eleq1d ssinss1 ad2antrr uninqs ad2antrl inopn 3expib com12 impcom eqeltrd
      an4 ex expdimp inex1 ralrimiv wb cpw elpw bicomi a1i anbi1d abbidv uniexg
      ssab2 qsexg pwexg 3syl ssexg syldan mpan istopg syl mpbird ) CUAIZAUOZDBU
      BZJZXMKZCIZLZAUCZUAIZGUOZXSJZYAKZXSIZMZGNZYAHUOZUDZXSIZHXSOZGXSOZLZXLYFYK
      XLYEGXLXMYAIZXRMANZYCXNJZYCKZCIZLZYBYDYNXRAYAOZXLYRXRAYAUEYSXOAYAOZXQAYAO
      ZLXLYRXOXQAYAUFXLYTUUAYRYTYOXLUUAYRMAYAXNUGXLYOUUAYRXLYOLZUUALZYOYQXLYOUU
      AUHUUCYPYGXPPZYMLZAUIZHUCZKZCHAYAUJUUCXLUUGCJZUUHCIXLYOUUAUKUUCUUFYGCIZMZ
      HNZUUIUUAUULUUBUUAYMXQMZANZUULXQAYAUEUUNUUKHUUNUUEUUJAUUMAUPUUJAQUUMUUEUU
      JMAUUMUUDYMUUJUUMYMUUDUUJXQUUDUUJMYMXPCYGULUMUNUQURUSUTVARUUFHCVGVBUUGCVC
      VDVEVFVHVIUQVJVIXRAYAVKXRYRAYCYAGVLZVMXMYCPZXOYOXQYQXMYCXNSUUPXPYPCXMYCVN
      VSVOVPVQUTXLYAXSIZYJMZGNYKXLUURGUUQYAXNJZYCCIZLZXLYJXRUVAAYAUVAAQUUOXMYAP
      ZXOUUSXQUUTXMYAXNSUVBXPYCCXMYAVNVSVOVRXLUVAYJXLUVALZYIHXSUVCYGXNJZYGKZCIZ
      LZYHXNJZYHKZCIZLZYGXSIYIXLUVAUVGUVKUVAUVGLUUSUVDLZUUTUVFLZLZXLUVKUUSUUTUV
      DUVFWIXLUVNUVKXLUVNLZUVHUVJUVNUVHXLUUSUVHUVDUVMYAYGXNVTWARUVOUVIYCUVEUDZC
      UVLUVIUVPPXLUVMDYAYGBFWBWCUVNXLUVPCIZUVMXLUVQMUVLXLUVMUVQXLUUTUVFUVQYCUVE
      CWDWEWFRWGWHVFWJVJWKXRUVGAYGUVGAQHVLXMYGPZXOUVDXQUVFXMYGXNSUVRXPUVECXMYGV
      NVSVOVRXRUVKAYHUVKAQYAYGUUOWLXMYHPZXOUVHXQUVJXMYHXNSUVSXPUVICXMYHVNVSVOVR
      VQWMWJVJUTYJGXSUEVBVFXLXSTIXTYLWNXLXSXMXNWOZIZXQLZAUCZTXLXRUWBAXLXOUWAXQX
      OUWAWNXLUWAXOXMXNAVLWPWQWRWSWTUWCUVTJZXLUWCTIZXQAUVTXBUWDXLUVTTIZUWEUWDXL
      LDTIZXNTIUWFXLUWGUWDXLDCKTECUAXAVERDBXCXNTXDXEUWCUVTTXFXGXHWHGHTXSXIXJXK
      $.
      $( [4-Jun-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.  $d L x $.
    $( If ` F ` is continous over two topologies ` J ` and ` K ` then it is
       continuous over ` ( J i^i K ) ` .  (Contributed by FL, 27-Nov-2011.) $)
    intcont $p |- ( ( ( J e. Top /\ K e. Top /\ L e. Top ) /\ U. J = U. K
    /\ ( F e. ( J Cn L ) /\ F e. ( K Cn L ) ) )
    -> F e. ( ( J i^i K ) Cn L ) ) $=
      ( vx ctop wcel w3a cuni wceq ccn co wa wf wral wb eqid iscn wi imp cin cv
      ccnv cima 3adant2 adantr 3adant1 anbi12d biimp3a pm3.2an3 3adant3 unint2t
      feq2 biimpd eqcoms 3syl adantrd elin simplbi2 ral2imi anim12d com12 mpcom
      a1i an4s inttop4 3ad2ant1 simp13 syl2anc mpbird ) BFGZCFGZDFGZHZBIZCIZJZA
      BDKLGZACDKLGZMZHZABCUAZDKLGZWBIZDIZANZAUCEUBUDZWBGZEDOZMZVOWEANZWGBGZEDOZ
      MZVPWEANZWGCGZEDOZMZMZWAWJVNVQVTWSVNVQMVRWNVSWRVNVRWNPZVQVKVMWTVLEABDVOWE
      VOQWEQZRUEUFVNVSWRPZVQVLVMXBVKEACDVPWEVPQXARUGUFUHUIWKWOWMWQWAWJSWAWKWOMZ
      WMWQMZMWJWAXCWFXDWIWAWKWFWOWAVKVLVQHZWDVOJWKWFSZVNVQXEVTVNVQXEVKVLVQXESZV
      MVKVLXGVKVLVQUJTUKTUKBCULXFVOWDVOWDJWKWFVOWDWEAUMUNUOUPUQXDWISWAWMWQWIWLW
      PWHEDWHWLWPWGBCURUSUTTVDVAVBVEVCWAWBFGZVMWCWJPVNVQXHVTVKVLXHVMBCVFUKVGVKV
      LVMVQVTVHEAWBDWDWEWDQXARVIVJ $.
      $( [27-Nov-2011] $)
  $}

  $c topX $.

  $( Extend class notation with a function whose value is a product
     topology. $)
  ctopx $a class topX $.

  ${
    $d f y t x i $.
    $( Note: does the intersection gives us the coarsest topology. Yes by
       iinss and inttop3 . It works with an empty family. The topology is
       { (/) } . $)
    $( The product topology of a family ` f ` of topologies is the coarsest
       topology over the product of the underlying sets that makes the
       projections continuous.  (Bourbaki TG I.14 ex. 3) Experimental. $)
    df-prtop $a |- topX = { <. f , y >. | ( f : dom f --> Top /\
       y = |^| { t e. Top | (
       U. t = X_ x e. dom f U. ( f ` x ) /\ A. i e. dom f
         ( X_ x e. dom f U. ( f ` x ) pr i ) e. ( t Cn ( f ` i ) ) ) } ) } $.
  $}

  ${
    $d A i $.  $d F i t x $.  $d I i t x $.
    $( Lemma for ~ usptop . $)
    usptoplem $p |- ( ( F : I --> Top /\ I e. A ) -> ~P X_ x e. I U. ( F ` x )
      e. { t e. Top | ( U. t = X_ x e. I U. ( F ` x ) /\
       A. i e. I ( X_ x e. I U. ( F ` x ) pr i ) e. ( t Cn ( F ` i ) ) ) } ) $=
      ( ctop wf wcel wa cv cfv cuni wceq co ccn wral cvv a1i sylancl cixp uniex
      cpro crab fvex rgen jctr adantl ixpexg distopg 3syl unipw ffvelrn adantlr
      cpw simpr eqid fveq2 unieqd prmapcp2 sylan simplr syl3anc ralrimiva jca32
      mapdiscn unieq eqeq1d opreq1 eleq2d ralbidv anbi12d elrab sylibr ) FGEHZF
      CIZJZAFAKZELZMZUAZUOZGIZWBMZWANZWADKZUCOZWBWFELZPOZIZDFQZJZJWBBKZMZWANZWG
      WMWHPOZIZDFQZJZBGUDIVQWCWEWKVQVPVTRIZAFQZJZWARIZWCVPXBVOVPXAWTAFWTVRFIVSV
      REUEUBSUFZUGUHAFVTCRUIZWARUJUKWEVQWAULSVQWJDFVQWFFIZJZWHGIZWAWHMZWGHZXCWJ
      VOXFXHVPFGWFEUMUNVQXCXFXJVQVPXAXCVOVPUPXDXETAFVTXIWAWFRWAUQVRWFNVSWHVRWFE
      URUSUTVAXGVPXAXCVOVPXFVBXDXETWAXIRWGWHXIUQVFVCVDVEWSWLBWBGWMWBNZWOWEWRWKX
      KWNWDWAWMWBVGVHXKWQWJDFXKWPWIWGWMWBWHPVIVJVKVLVMVN $.
      $( [16-Apr-202] $) $( [5-Dec-2011] $)
  $}

  ${
    $d A i $.  $d F f i t x y $.  $d I i t x $.
    $( Definition of the product topology of a family of topologies ` F ` .
       (Contributed by FL, 4-Dec-2011.) $)
    istopx $p |- ( ( F : I --> Top /\ I e. A )
      -> ( topX ` F ) = |^| { t e. Top | (
        U. t = X_ x e. I U. ( F ` x ) /\ A. i e. I
          ( X_ x e. I U. ( F ` x ) pr i ) e. ( t Cn ( F ` i ) ) ) } ) $=
      ( ctop wf wcel cfv cv cuni cixp wceq cpro co ccn wral wa cvv vf crab cint
      vy ctopx cdm wi fdm fex c0 wne wrex cpw elex adantl fvex uniex a1i ixpexg
      rgen sylancl distopg syl unipw ffvelrn adantlr simplr eqid fveq2 prmapcp2
      unieqd sylancom 3jca ex mapdiscn syl6 ralrimiv unieq eqeq1d opreq1 eleq2d
      w3a ralbidv anbi12d syl12anc rabn0 sylibr intex sylib simpl df-prtop feq1
      rcla4ev feq2d bitrd wb fveq1 adantr ralrimiva ixpeq2 ixpeq1 eqtrd opreq1d
      dmeq eqeq2d opreq2d eleq12d raleqbidv rabbidva fvopab6 syl3anc feq2 eleq1
      inteqd eleq1d raleqbi1dv rabbidv imbi12d mpbii anabsi5 ) FGEHZFCIZEUEJZBK
      ZLZAFAKZEJZLZMZNZYIDKZOPZYDYKEJZQPZIZDFRZSZBGUBZUCZNZYAEUFZFNZYAYBSZYTUGZ
      FGEUHUUBUUAGEHZUUACIZSZYCYEAUUAYHMZNZUUHYKOPZYNIZDUUARZSZBGUBZUCZNZUGUUDU
      UGETIUUOTIZUUEUUPUUAGCEUIUUGUUNUJUKZUUQUUGUUMBGULZUURUUGUUHUMZGIZUUTLZUUH
      NZUUJUUTYMQPZIZDUUARZUUSUUGUUHTIZUVAUUGUUATIZYHTIZAUUARZUVGUUFUVHUUEUUACU
      NUOUVIAUUAUVIYFUUAIYGYFEUPUQURUTZAUUAYHTTUSVAUUHTVBVCUVCUUGUUHVDURUUGUVED
      UUAUUGYKUUAIZYMGIZUUHYMLZUUJHZUVGWBZUVEUUGUVLUVPUUGUVLSZUVMUVOUVGUUEUVLUV
      MUUFUUAGYKEVEVFUUGUVLUVGUVOUVQUUFUVJUVGUUEUUFUVLVGUVKAUUAYHCTUSVAZAUUAYHU
      VNUUHYKTUUHVHYFYKNYGYMYFYKEVIVKVJVLUVRVMVNUUHUVNTUUJYMUVNVHVOVPVQUUMUVCUV
      FSBUUTGYDUUTNZUUIUVCUULUVFUVSYEUVBUUHYDUUTVRVSUVSUUKUVEDUUAUVSYNUVDUUJYDU
      UTYMQVTWAWCWDWMWEUUMBGWFWGUUNWHWIUUEUUFWJUAKZUFZGUVTHZUUEUAUDEYEAUWAYFUVT
      JZLZMZNZUWEYKOPZYDYKUVTJZQPZIZDUWARZSZBGUBZUCUUOTTUEAUDBUADWKUVTENZUWBUWA
      GEHUUEUWAGUVTEWLUWNUWAUUAGEUVTEXDZWNWOUWNUWMUUNUWNUWLUUMBGUWNUWLUUMWPYDGI
      UWNUWFUUIUWKUULUWNUWEUUHYEUWNUWEAUWAYHMZUUHUWNUWDYHNZAUWARUWEUWPNUWNUWQAU
      WAUWNUWQYFUWAIUWNUWCYGYFUVTEWQVKWRWSAUWAUWDYHWTVCUWNUWAUUANUWPUUHNUWOAUWA
      UUAYHXAVCXBZXEUWNUWJUUKDUWAUUAUWOUWNUWGUUJUWIYNUWNUWEUUHYKOUWRXCUWNUWHYMY
      DQYKUVTEWQXFXGXHWDWRXIXNXJXKUUBUUGUUCUUPYTUUBUUEYAUUFYBUUAFGEXLUUAFCXMWDU
      UBUUOYSYCUUBUUNYRUUBUUMYQBGUUBUUIYJUULYPUUBUUHYIYEAUUAFYHXAZXEUUKYODUUAFU
      UBUUJYLYNUUBUUHYIYKOUWSXCXOXPWDXQXNXEXRXSVCXT $.
      $( [16-Apr-202] $) $( [4-Dec-2011] $)
  $}

  ${
    $d x t i F $.  $d x t i I $.  $d x t i J $.  $d x X a $.  $d A t i $.
    $d i I a $.
    istopxc.1 $e |- X = U. J $.
    $( Product of ` I ` topology ` J ` .  (Contributed by FL, 15-Sep-2013.) $)
    istopxc $p |- ( ( F : I --> { J } /\ I e. A /\ J e. Top )
      -> ( topX ` F ) = |^| { t e. Top | (
        U. t = ( X ^m I ) /\ A. i e. I
          ( ( X ^m I ) pr i ) e. ( t Cn J ) ) } ) $=
      ( vx va wcel ctop cfv cv cixp wceq co wral wa cmap csn wf ctopx cuni cpro
      w3a ccn crab wss snssi sylan2 3adant2 simp2 istopx syl2anc fvconst unieqd
      cint fss syl6eqr ex ralrimiv 3ad2ant1 ixpeq2 syl cvv uniexg ixpeq1 opreq2
      syl5eqel eqeq12d ax-1 opreq1 ixpconst vtocl2g 3adant1 eqtrd eqeq2d adantr
      vex opreq1d 3ad2antl1 opreq2d eleq12d ralbidva anbi12d rabbidv inteqd ) E
      FUAZDUBZEBKZFLKZUFZDUCMZANZUDZIEINZDMZUDZOZPZWTCNZUEQZWOXBDMZUGQZKZCERZSZ
      ALUHZURZWPGETQZPZXKXBUEQZWOFUGQZKZCERZSZALUHZURWMELDUBZWKWNXJPWJWLXSWKWLW
      JWILUIXSFLUJEWILDUSUKULWJWKWLUMIABCDEUNUOWMXIXRWMXHXQALWMXAXLXGXPWMWTXKWP
      WMWTIEGOZXKWMWSGPZIERZWTXTPZWJWKYBWLWJYAIEWJWQEKZYAWJYDSZWSFUDZGYEWRFEFWQ
      DUPUQHUTVAVBVCZIEWSGVDZVEWKWLXTXKPZWJWLWKGVFKYIWLGYFVFHFLVGVJIXBJNZOZYJXB
      TQZPIEYJOZYJETQZPYICJEGBVFXBEPYKYMYLYNIXBEYJVHXBEYJTVIVKYJGPZYMXTYNXKYOYO
      IERYMXTPYOYOIEYOYDVLVBIEYJGVDVEYJGETVMVKIXBYJCVTJVTVNVOUKVPZVQVRWMXFXOCEW
      MXBEKZSZXCXMXEXNYRWTXKXBUEYRWTXTXKYRYBYCWMYBYQYGVSYHVEWMYIYQYPVSVQWAYRXDF
      WOUGWJWKYQXDFPWLEFXBDUPWBWCWDWEWFWGWHVQ $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d A i t $.  $d F i t x $.  $d I i t x $.
    $( The product topology of a family ` F ` of topologies is a topology.
       (Contributed by FL, 5-Dec-2011.) $)
    prtoptop $p |- ( ( F : I --> Top /\ I e. A )
      -> ( topX ` F ) e. Top ) $=
      ( vt vx vi ctop wcel wa cfv cv cuni wceq co ccn wral c0 wi ex cvv wf cixp
      ctopx cpro crab cint istopx wne wss wrex csn cpr cun 0ex p0ex unipr uncom
      ixp0x eqtr4i 3eqtri ral0 pm3.2i indistop unieq ixpeq1 eqeqan12rd wb eleq2
      un0 adantr opreq1d opreq1 eleq12d imbi12d ralbidv2 anbi12d rcla4edv com13
      adantl mp2 a1d wn cpw simprr fvex a1i rgen ixpexg sylancl distopg ffvelrn
      uniex ad2antrl imp simplrr eqid unieqd prmapcp2 sylancom mapdiscn syl3anc
      fveq2 ralrimiva unipw jctil eqeq1d eleq2d ralbidv mpid com12 3syl pm2.43i
      pm2.61i rabn0 sylibr ssrab2 inttop3 eqeltrd ) CGBUAZCAHZIZBUCJDKZLZECEKZB
      JZLZUBZMZYGFKZUDNZYBYIBJZONZHZFCPZIZDGUEZUFZGEDAFBCUGYAYPQUHZYPGUIYQGHYAY
      ODGUJZYRCQMZYAYSRYTYSYAQQUKZULZLZEQYFUBZMZUUDYIUDNZUUBYKONZHZFQPZIZUUBGHZ
      YTYSRUUEUUIUUCQUUAUMUUAQUMZUUDQUUAUNUOUPQUUAUQUULUUAUUDUUAVIEYFURUSUTUUHF
      VAVBUUAVCYTUUKUUJYSYTUUKUUJYSRYTYOUUJDUUBGYTYBUUBMZIZYHUUEYNUUIUUMYTYCUUC
      YGUUDYBUUBVDECQYFVEZVFUUNYMUUHFCQUUNYICHZYIQHZYMUUHYTUUPUUQVGUUMCQYIVHVJU
      UNYJUUFYLUUGYTYJUUFMUUMYTYGUUDYIUDUUOVKVJUUMYLUUGMYTYBUUBYKOVLVSVMVNVOVPV
      QSVRVTWAYTWBZYAYSUURYAIZYSUUSYGTHZYGWCZGHZUUSYSRUUSXTYFTHZECPZUUTUURXSXTW
      DUVCECUVCYDCHYEYDBWEWLWFWGZECYFATWHZWIYGTWJUUSUVBYSUUSUVBUVALZYGMZYJUVAYK
      ONZHZFCPZIZYSUUSUVKUVHUUSUVJFCUUSUUPIZYKGHZYGYKLZYJUAZUUTUVJUUSUUPUVNXSUU
      PUVNRUURXTXSUUPUVNCGYIBWKSWMWNUUSUUPUUTUVPUVMXTUVDUUTUURXSXTUUPWOUVEUVFWI
      ZECYFUVOYGYITYGWPYDYIMYEYKYDYIBXBWQWRWSUVQYGUVOTYJYKUVOWPWTXAXCYGXDXEUUSU
      VBUVLYSRUUSYOUVLDUVAGUUSYBUVAMZIZYHUVHYNUVKUVSYCUVGYGUVRYCUVGMUUSYBUVAVDV
      SXFUVRYNUVKVGUUSUVRYMUVJFCUVRYLUVIYJYBUVAYKOVLXGXHVSVPVQSXIXJXKXLSXMYODGX
      NXOYODGXPYPXQWIXR $.
      $( [5-Dec-2011] $)
  $}

  ${
    $d A i $.  $d F a i t x $.  $d I a i t x $.
    $( The underlying set of a product topology.  (Contributed by FL,
       5-Dec-2011.) $)
    usptop $p |- ( ( F : I --> Top /\ I e. A )
      -> U. ( topX ` F ) = X_ x e. I U. ( F ` x ) ) $=
      ( vt vi va ctop wf wcel wa ctopx cfv cuni cv wceq co ccn wral a1i cpw wss
      cixp cpro crab istopx unieqd ssrab2 usptoplem unieq eqeq1d opreq1 ralbidv
      cint weq eleq2d anbi12d elrab unipw eqcomi eqeq2i biimpi ad2antrl intfmu2
      sylbi rgen syl3anc 3eqtrd ) DHCIDBJKZCLMZNEOZNZADAOCMNUCZPZVMFOZUDQZVKVOC
      MZRQZJZFDSZKZEHUEZUNZNZVMUAZNZVMVIVJWCAEBFCDUFUGVIWBHUBZWEWBJGOZNZWFPZGWB
      SZWDWFPWGVIWAEHUHTAEBFCDUIWKVIWJGWBWHWBJWHHJZWIVMPZVPWHVQRQZJZFDSZKZKWJWA
      WQEWHHEGUOZVNWMVTWPWRVLWIVMVKWHUJUKWRVSWOFDWRVRWNVPVKWHVQRULUPUMUQURWMWJW
      LWPWMWJVMWFWIWFVMVMUSZUTVAVBVCVEVFTGWBWEVDVGWFVMPVIWSTVH $.
      $( [5-Dec-2011] $)
  $}

  ${
    $d A i o u $.  $d F i o u x $.  $d F i t u x $.  $d I i o u x $.
    $d I i t u x $.
    $( The projections are continuous.  (Contributed by FL, 18-Apr-2012.) $)
    prcnt $p |- ( ( F : I --> Top /\ I e. A ) -> A. i e. I
      ( X_ x e. I U. ( F ` x ) pr i ) e. ( ( topX ` F ) Cn ( F ` i ) ) ) $=
      ( vo vu vt ctop wcel wa cv cuni co ccn wral wb cvv wceq wi cfv cixp ctopx
      wf cpro ccnv cima prtoptop adantr ffvelrn adantlr eqid iscn syl2anc uniex
      simplr fvex a1i rgen ixpexg sylancl fveq2 unieqd prmapcp2 sylancom usptop
      feq2d mpbird wal w3a simprl ad2antrr ra4 ad2antll com12 ad2antlr imp 3jca
      jca cnima syl6 alrimiv crab cint istopx eleq2d oprex cnvexg imaexg elintg
      ex 3syl df-ral syl6bb syl unieq eqeq1d opreq1 ralbidv anbi12d elrab albii
      imbi1i 3bitrd ralrimiv mpbir2and ) EIDUDZEBJZKZAEALZDUAZMZUBZCLZUENZDUCUA
      ZXNDUAZONJZCEXIXNEJZXRXIXSKZXRXPMZXQMZXOUDZXOUFZFLZUGZXPJZFXQPZXTXPIJZXQI
      JZXRYCYHKQXIYIXSBDEUHUIXGXSYJXHEIXNDUJUKZFXOXPXQYAYBYAULYBULUMUNXTYCXMYBX
      OUDZXIXSXMRJZYLXTXHXLRJZAEPYMXGXHXSUPYNAEYNXJEJXKXJDUQUOURUSAEXLBRUTVAAEX
      LYBXMXNRXMULXJXNSXKXQXJXNDVBVCVDVEXTYAXMYBXOXIYAXMSXSABDEVFUIVGVHXTYGFXQX
      TYEXQJZYGXTYOKZYGGLZIJZYQMZXMSZXOYQXQONZJZCEPZKZKZYFYQJZTZGVIZYPUUGGYPUUE
      YRYJUUBVJZYOKZUUFYPUUEUUJYPUUEKZUUIYOUUKYRYJUUBYPYRUUDVKXTYJYOUUEYKVLYPUU
      EUUBXSUUEUUBTXIYOUUEXSUUBUUCXSUUBTYRYTUUBCEVMVNVOVPVQVRXTYOUUEUPVSWKYEXOY
      QXQVTWAWBYPYGYFHLZMZXMSZXOUULXQONZJZCEPZKZHIWCZWDZJZYQUUSJZUUFTZGVIZUUHXI
      YGUVAQXSYOXIXPUUTYFAHBCDEWEWFVLYPYFRJZUVAUVDQYPXORJZYDRJUVEUVFYPXMXNUEWGU
      RXORWHYDYERWIWLUVEUVAUUFGUUSPUVDGYFUUSRWJUUFGUUSWMWNWOUVDUUHQYPUVCUUGGUVB
      UUEUUFUURUUDHYQIUULYQSZUUNYTUUQUUCUVGUUMYSXMUULYQWPWQUVGUUPUUBCEUVGUUOUUA
      XOUULYQXQOWRWFWSWTXAXCXBURXDVHWKXEXFWKXE $.
      $( [18-Apr-2012] $)
  $}

