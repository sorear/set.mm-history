$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Initial and final topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c subSp $. $( Function returning a subspace topology. $)

  $( Extend class notation with the function returning a subspace topology. $)
  csubsp $a class subSp $.


  ${
    $d x y z u v $.
    $( Function returning the subspace topology induced by the topology
       ` y ` and the set ` x ` . $)
    df-subsp $a |- subSp = { <. <. x , y >. , z >. |
       ( y e. Top /\ z = { u | E. v e. y u = ( v i^i x ) } ) } $.
  $}


  ${
    $d A u v x y z $.  $d J u v x y z $.
    subsp.1 $e |- J e. Top $.
    subsp.2 $e |- A e. V $.
    $( The subspace topology induced by the topology ` J ` on the set
       ` A ` . $)
    subsp $p |- ( subSp ` <. A , J >. ) = { u | E. v e. J u = ( v i^i A ) }
      $=
      ( vx vy vz csubsp co cv cvv wcel ctop wa cin wceq wrex cab copab2 cop
      cfv df-subsp wb visset ibar anbi1d ax-mp oprabbii eqtri opreqi df-opr
      wss id inss2 a1i eqsstrd pm4.71ri rexbii r19.42v bitri abbii abssexg
      eqeltri ineq2 eqeq2d rexbidv abbidv rexeq1 eqid oprabval2 mp2an 3eqtr3i
      ) CDJKCDGLZMNZHLZONZPZILBLZALZVOQZRZAVQSZBTZRZPZGHIUAZKZCDUBJUCVTWACQZRZA
      DSZBTZJWHCDJVRWFPZGHIUAWHGHIABUDWNWGGHIVPWNWGUEGUFVPVRVSWFVPVRUGUHUIUJUKU
      LCDJUMCMNZDONWIWMRFEGHICDMOWEWMWHWKAVQSZBTWMVTCUNZWLPZBTZMWLWRBWLWQWKPZAD
      SWRWKWTADWKWQWKVTWJCWKUOWJCUNWKWACUPUQURUSUTWQWKADVAVBVCWOWSMNFWLBCMVDUIV
      EVOCRZWDWPBXAWCWKAVQXAWBWJVTVOCWAVFVGVHVIVQDRWPWLBWKAVQDVJVIWHVKVLVMVN $.
      $( [4-Jun-2007] $) $( [29-May-2007] $)
  $}

  ${ $d A u v $. $d B u v $. $d J u v $.
    issubsp.1 $e |- B e. V $.
    issubsp.2 $e |- J e. Top $.
    $( The predicate "is a subspace topology". $)
    issubsp $p |- ( A e. C -> ( A e. ( subSp ` <. B , J >. )
      <-> E. v e. J A = ( v i^i B ) ) ) $=
      ( vu wcel cop csubsp cfv cv cin wceq wrex cab subsp a1i eleq2d eqeq1
      rexbidv elabg bitrd ) BDIZBCEJKLZIBHMZAMCNZOZAEPZHQZIBUHOZAEPZUEUFUKBUFUK
      OUEAHCEGFRSTUJUMHBDUGBOUIULAEUGBUHUAUBUCUD $.
      $( [22-Dec-2008] $) $( [22-Dec-2008] $)
  $}

  ${ $d A v $. $d B v $. $d J v $.
    issubsplem1.1 $e |- J e. Top $.
    $( The predicate "is a subspace topology". $)
    issubsplem1 $p |- ( ( A e. C /\ B e. V ) -> ( A e. ( subSp ` <. B , J >. )
      <-> E. v e. J A = ( v i^i B ) ) ) $=
      ( cvv wcel cop csubsp cfv cv cin wceq wrex wb wi c0 cif opeq1 fveq2d
      eleq2d ineq2 eqeq2d rexbidv bibi12d imbi2d 0ex elimel issubsp dedth
      impcom ) CGHZBDHZBCEIZJKZHZBALZCMZNZAEOZPZUMUNVBQUNBUMCRSZEIZJKZHZBURVCMZ
      NZAEOZPZQCRCVCNZVBVJUNVKUQVFVAVIVKUPVEBVKUOVDJCVCETUAUBVKUTVHAEVKUSVGBCVC
      URUCUDUEUFUGABVCDECRGUHUIFUJUKUL $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${ $d A v $. $d B v $. $d J v $.
    $( The predicate "is an open set of a subspace topology". $)
    issubspt $p |- ( ( J e. Top /\ A e. C /\ B e. V ) ->
      ( A e. ( subSp ` <. B , J >. ) <-> E. v e. J A = ( v i^i B ) ) ) $=
      ( ctop wcel cvv cop csubsp cfv cv cin wceq wrex wb wi c0 csn cif opeq2
      fveq2d eleq2d rexeq1 bibi12d imbi2d sn0top elimel issubsplem1 ex dedth
      3imp ) EFGZBDGZCHGZBCEIZJKZGZBALCMNZAEOZPZUMUNUOVAQZQUNUOBCUMERSZTZIZJKZG
      ZUSAVDOZPZQZQEVCEVDNZVBVJUNVKVAVIUOVKURVGUTVHVKUQVFBVKUPVEJEVDCUAUBUCUSAE
      VDUDUEUFUFUNUOVIABCDVDEVCFUGUHUIUJUKUL $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${ $d J x y $.
    subspemp.1 $e |- J e. Top $.
    $( The subspace topology induced by the topology ` J ` on the empty set. $)
    subspemp $p |- ( subSp ` <. (/) , J >. ) = { (/) } $=
      ( vx vy cv c0 cin wceq wrex cab cop csubsp cfv csn wi wcel in0 eqtr
      mpan2 a1i r19.23aiv ctop 0opn ax-mp inidm eqcomi ineq1 eqeq2d rcla4ev
      mp2an eqeq1 rexbidv mpbiri impbii abbii 0ex subsp df-sn 3eqtr4i ) CEZDEZF
      GZHZDAIZCJUTFHZCJFAKLMFNVDVECVDVEVCVEDAVCVEOVAAPVCVBFHVEVAQUTVBFRSTUAVEVD
      FVBHZDAIZFAPZFFFGZHZVGAUBPVHBAUCUDVIFFUEUFVFVJDFAVAFHVBVIFVAFFUGUHUIUJVEV
      CVFDAUTFVBUKULUMUNUODCFABUPUQCFURUS $.
      $( [22-Dec-2008] $) $( [22-Dec-2008] $)
  $}

  $( The subspace topology induced by the topology ` J ` on the empty set. $)
  subspemp2 $p |- ( J e. Top -> ( subSp ` <. (/) , J >. ) = { (/) } ) $=
    ( ctop wcel c0 cop csubsp cfv csn wceq cif opeq2 fveq2d eqeq1d sn0top
    elimel subspemp dedth ) ABCZDAEZFGZDHZIDRAUAJZEZFGZUAIAUAAUBIZTUDUAUESUCFAU
    BDKLMUBAUABNOPQ $.
    $( [22-Dec-2008] $) $( [22-Dec-2008] $)


  ${ $d A x y $. $d B x $.
    $( The only subspace topology induced by the topology ` { (/) } ` . $)
    sbtpsines $p |- ( A e. B -> ( subSp ` <. A , { (/)  } >. ) = { (/) } )
      $=
      ( vx vy wcel c0 csn cop csubsp cfv cv cin wceq wrex cvv wb elisset ctop
      sn0top visset issubspt mp3an12 syl wa wex df-rex elsn anbi1i exbii 0ex
      ineq1 eqeq2d incom in0 eqtri eqeq2i syl6bb ceqsexv 3bitri bitr4i eqrdv )
      ABEZCAFGZHIJZVCVBCKZVDEZVEDKZALZMZDVCNZVEVCEZVBAOEZVFVJPZABQVCREVEOEVLVMS
      CTDVEAOVCUAUBUCVJVEFMZVKVJVGVCEZVIUDZDUEVGFMZVIUDZDUEVNVIDVCUFVPVRDVOVQVI
      DFUGUHUIVIVNDFUJVQVIVEFALZMVNVQVHVSVEVGFAUKULVSFVEVSAFLFFAUMAUNUOUPUQURUS
      CFUGUTUQVA $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${ $d A x y $. $d J x y $.
    stoiglem.1 $e |- J e. Top $.
    stoiglem.2 $e |- A (_ U. J $.
    $( The underlying set of a subspace topology. $)
    stoiglem $p |- <. A , ( subSp ` <. A , J >. ) >. e. TopSp $=
      ( vy vx cop csubsp cfv ctps wcel ctop cuni wceq istps cv cin wrex cab
      cvv uniexg ax-mp ssexi subsp subtop eqeltri wss dfss mpbi incom ntunte
      3eqtri unieqi eqtr4i mpbir2an ) AABGHIZGJKUPLKAUPMZNAUPOUPEPFPAQNFBRESZLF
      EABCABMZBLKZUSTKCBLUAUBDUCUDZUTURLKCEFBAUEUBUFAURMZUQAAUSQZUSAQVBAUSUGAVC
      NDAUSUHUIAUSUJEFBAUKULUPURVAUMUNUO $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  $( The topological space build with a subspace topology. $)
  stoig $p |- ( ( J e. Top /\ A (_ U. J ) ->
   <. A , ( subSp ` <. A , J >. ) >. e. TopSp ) $=
    ( ctop wcel cuni wss wa cop csubsp cfv ctps c0 csn cif wceq opeq2 fveq2d
    opeq2d eleq1d id opeq1 opeq12d iftrue pm3.26 eqeltrd wn iffalse sn0top
    syl6eqel pm2.61i sseq1 unieq sseq2d sylan9bb pm3.27 syl5cbir mp2and 0ss
    mpbiri sylanc stoiglem dedth2v ) BCDZABEZFZGZAABHZIJZHZKDAAVFBLMZNZHZIJZHZK
    DVFALNZVOVKHZIJZHZKDBAVJLBVKOZVIVNKVSVHVMAVSVGVLIBVKAPQRSAVOOZVNVRKVTAVOVMV
    QVTTVTVLVPIAVOVKUAQUBSVOVKVFVKCDVFVKBCVFBVJUCZVCVEUDUEVFUFZVKVJCVFBVJUGZUHU
    IUJVFVOVKEZFZVFVOAOZVKBOZWEVFALUCWAWFWGGWEVEVFWFWEAWDFWGVEVOAWDUKWGWDVDAVKB
    ULUMUNVCVEUOUPUQVOLOZVKVJOZWEWBWHWIGWELVJEZFZWJURWHWELWDFWIWKVOLWDUKWIWDWJL
    VKVJULUMUNUSVFALUGWCUTUJVAVB $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)

  $( The underlying set of a subspace topology. $)
  stoig2 $p |- ( ( J e. Top /\ A (_ U. J ) ->
   U. ( subSp ` <. A , J >. ) = A ) $=
    ( ctop wcel cuni wss wa cop csubsp cfv ctps wceq stoig istps id eqcoms
    adantl sylbi syl ) BCDABEFGAABHIJZHKDZTEZALZABMUATCDZAUBLZGUCATNUEUCUDUCUBA
    UCOPQRS $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)


  $( A subspace topology is a topology. $)
  stoig3 $p |- ( ( J e. Top /\ A (_ U. J ) ->
    ( subSp ` <. A , J >. ) e. Top ) $=
    ( ctop wcel cuni wss wa cop csubsp cfv ctps stoig wceq istps pm3.26bi syl
    ) BCDABEFGAABHIJZHKDZQCDZABLRSAQEMAQNOP $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)

  ${ $d A x y $. $d J x y $. $d X x $.
    subtopsin2.1 $e |- X = U. J $.
    $( The subspace topology induced by a singleton. $)
    subtopsin2 $p |- ( ( J e. Top /\ A e. X ) ->
      ( subSp ` <. { A } , J >. ) = { (/) , { A } } ) $=
      ( vx vy ctop wcel wa csn cop csubsp cfv c0 cpr cv cin wceq wrex cvv wb
      issubspt pm3.26 visset a1i snex syl3anc wi wss snssi df-ss incom eqtr wo
      olc elpr sylibr syl ex syl5com mpan sylbi wn disjsn orc expcom sylbir
      pm2.61i r19.23aiv syl6bi eleq1 ctps cuni stoig eleq2i sylan2 istps 0opn
      adantr syl5bir eqid topopn biimpd eqcoms mpan9 jaoi com12 impbid eqrdv )
      BGHZACHZIZEAJZBKLMZNWMOZWLEPZWNHZWPWOHZWLWQWPFPZWMQZRZFBSZWRWJWPTHZWMTHZW
      QXBUAWLFWPWMTBUBWJWKUCXCWLEUDZUEXDWLAUFUEUGXAWRFBXAWRUHZWSBHAWSHZXFXGWMWS
      UIZXFAWSUJXHWMWSQZWMRZXFWMWSUKWTXIRZXJXFWSWMULXAWTWMRZWRXKXJIXAXLWRXAXLIW
      PWMRZWRWPWTWMUMXMWPNRZXMUNZWRXMXNUOWPNWMXEUPZUQURUSWTXIWMUMUTVAVBURXGVCWT
      NRZXFWSAVDXAXQWRXAXQIXNWRWPWTNUMXNXOWRXNXMVEXPUQURVFVGVHUEVIVJWRWLWQWRXOW
      LWQUHZXPXNXRXMXNWQNWNHZWLWPNWNVKWLWMWNKVLHZXSWJWMBVMZUIZXTWKWMBVNWKAYAHYB
      CYAADVOAYAUJVBVPZXTWNGHZWMWNVMZRZIZXSWMWNVQZYDXSYFWNVRVSVBURVTXMWQWMWNHZW
      LWPWMWNVKWLXTYIYCXTYGYIYHYDYEWNHZYFYIWNYEYEWAWBYJYIUHYEWMYEWMRYJYIYEWMWNV
      KWCWDWEVBURVTWFVBWGWHWI $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}


  ${ $d X x y z $.  $d J x y z $.  $d R x y z $.
    qusp.1 $e |- X = U. J $.
    qusp.2 $e |- Er R $.
    $( A quotient space is a topology. $)
    qusp $p |- ( J e. Top ->
      { x | ( x (_ ( X /. R ) /\ U. x e. J ) } e. Top ) $=
      ( vy vz ctop wcel cv cqs wss cuni wa cab wi wal cin wral simplr wceq wex
      uniopn simpll df-ral hba1 ax-17 eleq1a imim2i com23 imp3a a4s 19.23ad
      19.21aiv sylbi adantl abss sylibr sylanc uniuni syl5eqel jca exp31
      unissb syl5ibr r19.26 syl5ib ssab visset uniex sseq1 unieq eleq1d
      anbi12d elab 3imtr4g ssinss1 ad2antrr uninqs ad2antrl inopn 3expib com12
      impcom eqeltrd ex an4 expdimp elabf inex1 r19.21aiv cvv wb cpw elpw
      bicomi a1i anbi1d abbidv ssab2 uniexg qsexg pwexg 3syl ssexg syldan mpan
      istopg syl mpbird ) CIJZAKZDBLZMZXMNZCJZOZAPZIJZGKZXSMZYANZXSJZQZGRZYAHKZ
      SZXSJZHXSTZGXSTZOZXLYFYKXLYEGXLXMYAJZXRQARZYCXNMZYCNZCJZOZYBYDXLXRAYATZYR
      YNXLXOAYATZXQAYATZOYRYSXLYTUUAYRXLYOUUAYRQYTXLYOUUAYRXLYOOZUUAOZYOYQXLYOU
      UAUAUUCYGXPUBZYMOZAUCZHPZNZCYPXLUUGCMZUUHCJUUCUUGCUDXLYOUUAUEUUCUUFYGCJZQ
      ZHRZUUIUUAUULUUBUUAYMXQQZARZUULXQAYAUFUUNUUKHUUNUUEUUJAUUMAUGUUJAUHUUMUUE
      UUJQAUUMUUDYMUUJUUMYMUUDUUJXQUUDUUJQYMXPCYGUIUJUKULUMUNUOUPUQUUFHCURUSUTH
      AYAVAVBVCVDAYAXNVEVFULXOXQAYAVGVHXRAYAUFVFXRAYAVIXRYRAYCYAGVJZVKXMYCUBZXO
      YOXQYQXMYCXNVLUUPXPYPCXMYCVMVNVOVPVQUOXLYAXSJZYJQZGRYKXLUURGXLYAXNMZYCCJZ
      OZYJUUQXLUVAYJXLUVAOZYIHXSUVBYGXNMZYGNZCJZOZYHXNMZYHNZCJZOZYGXSJYIXLUVAUV
      FUVJXLUUSUVCOZUUTUVEOZOZUVJUVAUVFOXLUVMUVJXLUVMOZUVGUVIUVMUVGXLUUSUVGUVCU
      VLYAYGXNVRVSUQUVNUVHYCUVDSZCUVKUVHUVOUBXLUVLDYAYGBFVTWAUVMXLUVOCJZUVLXLUV
      PQUVKXLUVLUVPXLUUTUVEUVPYCUVDCWBWCWDUQWEWFVCWGUUSUUTUVCUVEWHVHWIXRUVFAYGU
      VFAUHHVJXMYGUBZXOUVCXQUVEXMYGXNVLUVQXPUVDCXMYGVMVNVOWJXRUVJAYHUVJAUHYAYGU
      UOWKXMYHUBZXOUVGXQUVIXMYHXNVLUVRXPUVHCXMYHVMVNVOWJVQWLWGXRUVAAYAUVAAUHUUO
      XMYAUBZXOUUSXQUUTXMYAXNVLUVSXPYCCXMYAVMVNVOWJVHUOYJGXSUFUSVCXLXSWMJXTYLWN
      XLXSXMXNWOZJZXQOZAPZWMXLXRUWBAXLXOUWAXQXOUWAWNXLUWAXOXMXNAVJWPWQWRWSWTUWC
      UVTMZXLUWCWMJZXQAUVTXAUWDXLUVTWMJZUWEUWDXLODWMJZXNWMJUWFXLUWGUWDXLCNWMDCI
      XBEVBUQDBXCXNWMXDXEUWCUVTWMXFXGXHWFGHWMXSXIXJXK $.
      $( [4-Jun-2007] $) $( [4-Jun-2007] $)
  $}

  ${
    $d A x $. $d J u v x z $. $d K u v x z $.
    eltpt.1 $e |- B = { z | E. u e. J E. v e. K z = ( u X. v ) } $.
    $( Condition for a set to belong to a product topology.. $)
    eltpt $p |- ( ( J e. Top /\ K e. Top ) -> ( A e. ( J tX K ) <-> E. x
       ( x (_ B /\ A = U. x ) ) ) $=
      ( ctop wcel wa ctx co cv cxp wceq wrex cab ctg cfv wss cuni wex txval
      eleq2d ctb wb txbas eltg3 syl sseq2i bicomi anbi1i exbii a1i 3bitrd ) GJK
      HJKLZEGHMNZKEBODOCOPQCHRDGRBSZTUAZKZAOZUTUBZEVCUCQZLZAUDZVCFUBZVELZAUDZUR
      USVAEBCDGHUEUFURUTUGKVBVGUHBCDGHUIAEUTUJUKVGVJUHURVFVIAVDVHVEVHVDFUTVCIUL
      UMUNUOUPUQ $.
      $( [18-Apr-2010] $) $( [18-Apr-2010] $)
  $}

  ${
    topgele.1 $e |- X = U. J $.
    $( The topologies over the same set have a greatest element (the discret
       toplogy) and a least element (the undiscrete topology). $)
    topgele $p |- ( J e. Top -> ( { (/) , X } (_ J /\ J (_ P~ X ) ) $=
      ( ctop wcel cuni cpw wss wa c0 cpr cop ctps istps2 eltopsp bitr3i wi
      wceq wb id eqcoms eleq1d anbi2d ax-mp prssg ibi sylbi pweq eqcomi sseq2i
      biimpi anim12i expcom a1i imp31 sylbir ) ADEZUQAAFZGZHZIJAEZURAEZIZIZJBKA
      HZABGZHZIZVDURALMEUQURANAOPUQUTVCVHUTVCVHQQUQVCUTVHVCVEUTVGVCVABAEZIZVEBU
      RRZVCVJSCVKVBVIVAVKURBAURBRZURBVLTUAUBUCUDVJVEJBAAAUEUFUGUTVGUSVFAVFUSVKV
      FUSRCBURUHUDUIUJUKULUMUNUOUP $.
      $( [18-Apr-2010] $) $( [18-Apr-2010] $)
  $}

  ${
    ptincpw.1 $e |- X = U. J $.
    ptincpw.2 $e |- Y = U. K $.
    $( A product topology is included in the powerset of the cross product
       of the underlying sets. The cross product and the empty set are
       elements of a product topology. $)
    ptincpw $p |- ( ( J e. Top /\ K e. Top )
      -> ( { (/) , ( X X. Y ) } (_ ( J tX K )
           /\ ( J tX K ) (_ P~ ( X X. Y ) ) ) $=
      ( ctx co cuni cxp wceq ctop wcel wa c0 cpr wss cpw eqid txuni preq2
      eqcoms sseq1d wb pweq sseq2d anbi12d txtop topindis syl syl5bir mpcom )
      ABGHZIZCDJZKZALMBLMNZOUOPZUMQZUMUORZQZNZABUMCDUMSZEFTUPVBOUNPZUMQZUMUNRZQ
      ZNZUQUPUSVEVAVGUPURVDUMURVDKUOUNUOUNOUAUBUCVAVGUDUOUNUOUNKUTVFUMUOUNUEUFU
      BUGUQUMLMVHABUMVCUHUMUIUJUKUL $.
      $( [18-Apr-2010] $) $( [18-Apr-2010] $)

  $}

  ${
    $d R u v w x z $. $d S u v w x z $. $d T u v w x z $.
    $d X u v w x z $. $d Y u v w x z $. $d Z u v w x z $.
    tx1cn.1 $e |- T = ( R tX S ) $.
    tx1cn.2 $e |- X = U. R $.
    tx1cn.3 $e |- Y = U. S $.
    tx1cn.4 $e |- Z = ( X X. Y ) $.
    $( Continuity of the first projection map of a topological product.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    tx1cn $p |- ( ( R e. Top /\ S e. Top ) -> ( 1st |` Z ) e. ( T Cn R ) ) $=
      ( vw vz vu vv ctop wcel wa c1st cres ccn co cuni wf ccnv cv cima wral
      cxp f1stres wceq a1i reseq2 syl feq1d wb txuni feq2 bitrd mpbiri wrex
      cab wss ctg cfv ctb txbas bastg ctx txval syl5eq sseqtr4d adantr xpeq1
      eqeq2d xpeq2 rcla42ev pm3.27 topopn ad2antlr crn imassrn sseli cdm cin
      cvv dfdm4 dmres eqtr3i wfo fo1st fof fdm ax-mp ineq2i inv1 3eqtri
      syl6eleq wi fvres eleq1d biimpd c2nd xpss xp2nd elxp7 biimpri sylan32c
      ex syldd com23 wfun ffun fvimacnvi mpan syl5 mpdi ssrdv eltopss xpss1
      sseld imp adantllr xp1st adantl eqeltrd r19.21aiva funimass4 adantlr
      fdmi syl6ssr sylancr mpbird funimass3 mpbid eqssd cnveqi imaeq1i syl3anc
      fex xpexg uniexg syl5eqel syl2an cnvexg imaexg 3syl eqeq1 2rexbidv elabg
      sseldd jca eqid iscn txtop pm3.26 sylanc ) AOPZBOPZQZRFSZCATUAPZCUBZDUUTU
      CZUUTUDZKUEZUFZCPZKAUGZQZUUSUVCUVHUUSUVCDEUHZDRUVJSZUCZDEUIZUUSUVCUVBDUVK
      UCZUVLUUSUVBDUUTUVKUUSFUVJUJZUUTUVKUJZUVOUUSJUKFUVJRULZUMUNUUSUVBUVJUJUVN
      UVLUOABCDEGHIUPUVBUVJDUVKUQUMURUSUUSUVGKAUUSUVEAPZQZLUEZMUEZNUEZUHZUJZNBU
      TMAUTZLVAZCUVFUUSUWFCVBUVRUUSUWFUWFVCVDZCUUSUWFVEPUWFUWGVBLNMABVFUWFVGUMU
      USABVHUAUWGCLNMABVIGVJVKVLUVSUVFUWFPZUVFUWCUJZNBUTMAUTZUVREBPZUVFUVEEUHZU
      JZUWJUVSUWIUWMUVFUVEUWBUHZUJMNUVEEABUWAUVEUJUWCUWNUVFUWAUVEUWBVMVNUWBEUJU
      WNUWLUVFUWBEUVEVOVNVPUUSUVRVQUURUWKUUQUVRBEIVRVSUVSUVKUDZUVEUFZUWLUVFUVSU
      WPUWLUVSLUWPUWLUVSUVTUWPPZUVTUVJPZUVTUWLPZUWQUVTUWOVTZUVJUWPUWTUVTUWOUVEW
      AWBUWTUVJRWCZWDZUVJWEWDUVJUVKWCZUWTUXBUVKWFRUVJWGWHUXAWEUVJWEWERWIZUXAWEU
      JZWJUXDWEWERUCUXEWEWERWKWEWERWLUMWMWNUVJWOWPWQUVSUVTUVKVDZUVEPZUWRUWSWRUW
      QUVSUWRUXGUWSUVSUWRUXGUVTRVDZUVEPZUWSUWRUXGUXIWRWRUVSUWRUXGUXIUWRUXFUXHUV
      EUVTUVJRWSZWTXAUKUWRUXIUWSWRWRUVSUWRUXIUWSUVTWEWEUHZPZUXIUVTXBVDEPZUWSUWR
      UXIQUWRUXLUXIUVJUXKUVTDEXCWBVLUWRUXIVQUWRUXMUXIUVTDEXDVLUWSUXLUXIUXMQQUVT
      UVEEXEXFXGXHUKXIXJUVKXKZUWQUXGUVLUXNUVMUVJDUVKXLWMZUVTUVEUVKXMXNXOXPXQUVS
      UVKUWLUFUVEVBZUWLUWPVBZUVSUXPUXGLUWLUGZUVSUXGLUWLUVSUWSQZUXFUXHUVEUXSUWRU
      XFUXHUJUUQUVRUWSUWRUURUUQUVRQZUWSUWRUXTUWLUVJUVTUXTUVEDVBUWLUVJVBZUVEADHX
      RUVEDEXSUMZXTYAYBUXJUMUWSUXIUVSUVTUVEEYCYDYEYFUXNUWLUXCVBZUXPUXRUOUVSLUWL
      UVEUVKYGUXOUVSUWLUVJUXCUUQUVRUYAUURUYBYHUVJDUVKUVMYIYJZYKYLUXNUYCUXPUXQUO
      UVSUWLUVEUVKYMUXOUYDYKYNYOUVDUWOUVEUUTUVKUVOUVPJUVQWMZYPYQVJYRUVSUVFWEPZU
      WHUWJUOUVSUUTWEPUVDWEPUYFUVSUVKWEUUTUUSUVKWEPZUVRUVLUVJWEPZUYGUUSUVJDWEUV
      KYSUVMDWEPEWEPUYHUUQUURDEWEWEYTUUQAUBWEDAOUUAHUUBUURBUBWEEBOUUAIUUBUUCYKV
      LUYEUUBUUTWEUUDUVDUVEWEUUEUUFUWEUWJLUVFWEUVTUVFUJUWDUWIMNABUVTUVFUWCUUGUU
      HUUIUMYLUUJYFUUKCOPUUQUVAUVIUOUUSKUUTCAUVBDUVBUULHUUMABCGUUNUUQUURUUOUUPY
      L $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R u v w x z $. $d S u v w x z $. $d T u v w x z $.
    $d X u v w x z $. $d Y u v w x z $. $d Z u v w x z $.
    tx2cn.1 $e |- T = ( R tX S ) $.
    tx2cn.2 $e |- X = U. R $.
    tx2cn.3 $e |- Y = U. S $.
    tx2cn.4 $e |- Z = ( X X. Y ) $.
    $( Continuity of the second projection map of a topological product.
       (Contributed by Jeff Madsen, 2-Sep-2009.)  $)
    tx2cn $p |- ( ( R e. Top /\ S e. Top ) -> ( 2nd |` Z ) e. ( T Cn S ) ) $=
      ( vw vz vu vv ctop wcel wa c2nd cres ccn co cuni wf ccnv cv cima wral
      cxp f2ndres wceq a1i reseq2 syl feq1d wb txuni feq2 bitrd mpbiri wrex
      cab wss ctg cfv ctb txbas bastg ctx txval syl5eq sseqtr4d adantr xpeq1
      eqeq2d xpeq2 rcla42ev topopn ad2antrr pm3.27 crn imassrn sseli cdm cin
      cvv dfdm4 dmres eqtr3i wfo fo2nd fof fdm ax-mp ineq2i inv1 3eqtri
      syl6eleq wi fvres eleq1d biimpd c1st xpss xp1st elxp7 biimpri sylan32c
      ex syldd com23 wfun ffun fvimacnvi mpan syl5 mpdi ssrdv eltopss xpss2
      sseld imp adantlll xp2nd adantl eqeltrd r19.21aiva funimass4 adantll
      fdmi syl6ssr sylancr mpbird funimass3 mpbid eqssd cnveqi imaeq1i syl3anc
      fex xpexg uniexg syl5eqel syl2an cnvexg imaexg 3syl eqeq1 2rexbidv elabg
      sseldd jca txtop eqid iscn sylancom ) AOPZBOPZQZRFSZCBTUAPZCUBZEUUSUCZUUS
      UDZKUEZUFZCPZKBUGZQZUURUVBUVGUURUVBDEUHZERUVISZUCZDEUIZUURUVBUVAEUVJUCZUV
      KUURUVAEUUSUVJUURFUVIUJZUUSUVJUJZUVNUURJUKFUVIRULZUMUNUURUVAUVIUJUVMUVKUO
      ABCDEGHIUPUVAUVIEUVJUQUMURUSUURUVFKBUURUVDBPZQZLUEZMUEZNUEZUHZUJZNBUTMAUT
      ZLVAZCUVEUURUWECVBUVQUURUWEUWEVCVDZCUURUWEVEPUWEUWFVBLNMABVFUWEVGUMUURABV
      HUAUWFCLNMABVIGVJVKVLUVRUVEUWEPZUVEUWBUJZNBUTMAUTZDAPZUVQUVEDUVDUHZUJZUWI
      UVRUWHUWLUVEDUWAUHZUJMNDUVDABUVTDUJUWBUWMUVEUVTDUWAVMVNUWAUVDUJUWMUWKUVEU
      WAUVDDVOVNVPUUPUWJUUQUVQADHVQVRUURUVQVSUVRUVJUDZUVDUFZUWKUVEUVRUWOUWKUVRL
      UWOUWKUVRUVSUWOPZUVSUVIPZUVSUWKPZUWPUVSUWNVTZUVIUWOUWSUVSUWNUVDWAWBUWSUVI
      RWCZWDZUVIWEWDUVIUVJWCZUWSUXAUVJWFRUVIWGWHUWTWEUVIWEWERWIZUWTWEUJZWJUXCWE
      WERUCUXDWEWERWKWEWERWLUMWMWNUVIWOWPWQUVRUVSUVJVDZUVDPZUWQUWRWRUWPUVRUWQUX
      FUWRUVRUWQUXFUVSRVDZUVDPZUWRUWQUXFUXHWRWRUVRUWQUXFUXHUWQUXEUXGUVDUVSUVIRW
      SZWTXAUKUWQUXHUWRWRWRUVRUWQUXHUWRUVSWEWEUHZPZUVSXBVDDPZUXHUWRUWQUXHQUWQUX
      KUXHUVIUXJUVSDEXCWBVLUWQUXLUXHUVSDEXDVLUWQUXHVSUWRUXKUXLUXHQQUVSDUVDXEXFX
      GXHUKXIXJUVJXKZUWPUXFUVKUXMUVLUVIEUVJXLWMZUVSUVDUVJXMXNXOXPXQUVRUVJUWKUFU
      VDVBZUWKUWOVBZUVRUXOUXFLUWKUGZUVRUXFLUWKUVRUWRQZUXEUXGUVDUXRUWQUXEUXGUJUU
      QUVQUWRUWQUUPUUQUVQQZUWRUWQUXSUWKUVIUVSUXSUVDEVBUWKUVIVBZUVDBEIXRUVDEDXSU
      MZXTYAYBUXIUMUWRUXHUVRUVSDUVDYCYDYEYFUXMUWKUXBVBZUXOUXQUOUVRLUWKUVDUVJYGU
      XNUVRUWKUVIUXBUUQUVQUXTUUPUYAYHUVIEUVJUVLYIYJZYKYLUXMUYBUXOUXPUOUVRUWKUVD
      UVJYMUXNUYCYKYNYOUVCUWNUVDUUSUVJUVNUVOJUVPWMZYPYQVJYRUVRUVEWEPZUWGUWIUOUV
      RUUSWEPUVCWEPUYEUVRUVJWEUUSUURUVJWEPZUVQUVKUVIWEPZUYFUURUVIEWEUVJYSUVLDWE
      PEWEPUYGUUPUUQDEWEWEYTUUPAUBWEDAOUUAHUUBUUQBUBWEEBOUUAIUUBUUCYKVLUYDUUBUU
      SWEUUDUVCUVDWEUUEUUFUWDUWILUVEWEUVSUVEUJUWCUWHMNABUVSUVEUWBUUGUUHUUIUMYLU
      UJYFUUKUUPUUQCOPUUTUVHUOABCGUULKUUSCBUVAEUVAUUMIUUNUUOYL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A h x y z $. $d B h x y z $. $d C h x y z $. $d D h x y z $.
    $d F h x y z $. $d G h x y z $. $d P h x y z $. $d Q h x y z $.
    upxp.1 $e |- P = ( 1st |` ( B X. C ) ) $.
    upxp.2 $e |- Q = ( 2nd |` ( B X. C ) ) $.
    $( Universal property of the Cartesian product considered as a categorical
       product in the category of sets. (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    upxp $p |- ( ( A e. D /\ F : A --> B /\ G : A --> C ) ->
        E! h ( h : A --> ( B X. C ) /\ F = ( P o. h ) /\ G = ( Q o. h ) ) ) $=
      ( vx vy vz wcel wf w3a cxp cv ccom wceq weu cfv cop wa copab cvv
      opabex2g eueq sylib 3ad2ant1 wral eqidd c1st c2nd ffvelrn xpss sseli syl
      3ad2antl1 adantll cres fveq1 coeq1i fveq1i syl6eq 3ad2ant2 ad2antlr wfun
      cdm fvco wfn wss wfo fo1st fofn ax-mp ssv fnssres mp2an fnfun a1i ffun
      fdm eleq2d biimpar syl3anc fvres 3eqtrrd 3ad2ant3 fo2nd eqopi sylan32c
      fveq2 opeq12d eqid opex fvopab4 adantl eqtr4d r19.21aiva jca wb eqfnfv
      ffn opelxpi syl2an anandirs 3adant1 fopab2 adantr sylanc mpbird ex feq1
      coeq2 eqeq2d 3anbi123d fveq2d 3adantl1 fvex op1st 3eqtrd eqtr2d crn fnco
      frn syl6eqr op2nd 3jca syl5cbir impbid eubidv ) ADOZABHPZACIPZQZABCRZGSZP
      ZHEYSTZUAZIFYSTZUAZQZGUBYSLSZAOZMSUUFHUCZUUFIUCZUDZUAUELMUFZUAZGUBZYNYOUU
      MYPYNUUKUGOUUMLMAUUJDUHGUUKUIUJUKYQUUEUULGYQUUEUULYQUUEUULYQUUEUEZUULAAUA
      ZNSZYSUCZUUPUUKUCZUAZNAULZUEZUUNUUOUUTUUNAUMUUNUUSNAUUNUUPAOZUEZUUQUUPHUC
      ZUUPIUCZUDZUURUUQUGUGRZOZUUQUNUCZUVDUAUUQUOUCZUVEUAUUQUVFUAUVCUUEUVBUVHYQ
      YTUUBUVBUVHUUDYTUVBUEUUQYROZUVHAYRUUPYSUPZYRUVGUUQBCUQURUSUTVAUVCUVDUUPUN
      YRVBZYSTZUCZUUQUVMUCZUVIUUEUVDUVOUAZYQUVBUUBYTUVQUUDUUBUVDUUPUUAUCUVOUUPH
      UUAVCUUPUUAUVNEUVMYSJVDVEVFVGVHUVMVIZYSVIZUUPYSVJZOZUVOUVPUAUVCUUPUVMYSVK
      UVRUVCUVMYRVLZUVRUNUGVLZYRUGVMZUWBUGUGUNVNUWCVOUGUGUNVPVQYRVRZUGYRUNVSVTZ
      YRUVMWAVQZWBUUEUVSYQUVBYTUUBUVSUUDAYRYSWCUKVHZUUEUVBUWAYQYTUUBUVBUWAUUDYT
      UWAUVBYTUVTAUUPAYRYSWDWEWFUTVAZWGUVCUVKUVPUVIUAUUEUVBUVKYQYTUUBUVBUVKUUDU
      VLUTVAZUUQYRUNWHUSWIUVCUVEUUPUOYRVBZYSTZUCZUUQUWKUCZUVJUUEUVEUWMUAZYQUVBU
      UDYTUWOUUBUUDUVEUUPUUCUCUWMUUPIUUCVCUUPUUCUWLFUWKYSKVDVEVFWJVHUWKVIZUVSUW
      AUWMUWNUAUVCUUPUWKYSVKUWPUVCUWKYRVLZUWPUOUGVLZUWDUWQUGUGUOVNUWRWKUGUGUOVP
      VQUWEUGYRUOVSVTZYRUWKWAVQZWBUWHUWIWGUVCUVKUWNUVJUAUWJUUQYRUOWHUSWIUUQUVDU
      VEWLWMUVBUURUVFUAZUUNLMUUPUUJUVFAUUKUUFUUPUAUUHUVDUUIUVEUUFUUPHWNUUFUUPIW
      NWOUUKWPZUVDUVEWQWRZWSWTXAXBYSAVLZUUKAVLZUULUVAXCUUNNAAYSUUKXDUUEUXDYQYTU
      UBUXDUUDAYRYSXEUKWSYQUXEUUEYQAYRUUKPZUXEYQUUJYROZLAULZUXFYOYPUXHYNYOYPUEU
      XGLAYOYPUUGUXGUUHBOUUICOUXGYOUUGUEYPUUGUEUUHUUIBCXFABUUFHUPACUUFIUPXGXHXA
      XILMAYRUUJUUKUXBXJUJZAYRUUKXEUSZXKXLXMXNUULUUEUXFHEUUKTZUAZIFUUKTZUAZQYQU
      ULYTUXFUUBUXLUUDUXNAYRYSUUKXOUULUUAUXKHYSUUKEXPXQUULUUCUXMIYSUUKFXPXQXRYQ
      UXFUXLUXNUXIYQHUVMUUKTZUXKYQHUXOUAZUUOUVDUUPUXOUCZUAZNAULZUEZYQUUOUXSYQAU
      MZYQUXRNAYQUVBUEZUXQUURUVMUCZUVDUVRUUKVIZUUPUUKVJZOZUXQUYCUAUYBUUPUVMUUKV
      KUVRUYBUWGWBUYBUXFUYDYQUXFUVBUXIXKAYRUUKWCUSZYQUYFUVBYQUYEAUUPYQUXFUYEAUA
      UXIAYRUUKWDUSWEWFZWGUYBUYCUVFUVMUCZUVFUNUCZUVDUYBUURUVFUVMUVBUXAYQUXCWSZX
      SUYBUVFYROZUYIUYJUAYOYPUVBUYLYNYOYPUVBUYLUVDBOUVECOUYLYOUVBUEYPUVBUEUVDUV
      EBCXFABUUPHUPACUUPIUPXGXHXTZUVFYRUNWHUSUYJUVDUAUYBUVDUVEUUPHYAZYBWBYCYDXA
      XBHAVLZUXOAVLZUXPUXTXCYQNAAHUXOXDYOYNUYOYPABHXEVGUWBUXEUUKYEYRVMZUYPYQYRA
      UVMUUKYFUWBYQUWFWBUXJYQUXFUYQUXIAYRUUKYGUSZWGXLXMEUVMUUKJVDYHYQIUWKUUKTZU
      XMYQIUYSUAZUUOUVEUUPUYSUCZUAZNAULZUEZYQUUOVUCUYAYQVUBNAUYBVUAUURUWKUCZUVE
      UWPUYDUYFVUAVUEUAUYBUUPUWKUUKVKUWPUYBUWTWBUYGUYHWGUYBVUEUVFUWKUCZUVFUOUCZ
      UVEUYBUURUVFUWKUYKXSUYBUYLVUFVUGUAUYMUVFYRUOWHUSVUGUVEUAUYBUVDUVEUYNUUPIY
      AYIWBYCYDXAXBIAVLZUYSAVLZUYTVUDXCYQNAAIUYSXDYPYNVUHYOACIXEWJUWQUXEUYQVUIY
      QYRAUWKUUKYFUWQYQUWSWBUXJUYRWGXLXMFUWKUUKKVDYHYJYKYLYMXM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R h s t u v w x y z $. $d S h s t u v w x y z $. $d X h s t u v w x y z $.
    $d T h s t u v w x y z $. $d U h s t u v w x y z $. $d Y h s t u v w x y z $.
    $d P h s t u v w x y z $. $d Q h s t u v w x y z $. $d Z h s t u v w x y z $.
    $d F h s t u v w x y z $. $d G h s t u v w x y z $.
    uptx.1 $e |- T = ( R tX S ) $.
    uptx.2 $e |- X = U. R $.
    uptx.3 $e |- Y = U. S $.
    uptx.4 $e |- Z = ( X X. Y ) $.
    uptx.5 $e |- P = ( 1st |` Z ) $.
    uptx.6 $e |- Q = ( 2nd |` Z ) $.
    $( Universal property of the binary topological product.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    uptx $p |- ( ( ( R e. Top /\ S e. Top /\ U e. Top ) /\
                          ( F e. ( U Cn R ) /\ G e. ( U Cn S ) ) ) ->
                  E! h e. ( U Cn T ) ( F = ( P o. h ) /\ G = ( Q o. h ) ) ) $=
      ( vx vy vz vs vw vu vv vt cv ccom wceq wa ccn co wreu wcel wex wmo ctop
      w3a weu df-reu eu5 bitri cuni cfv cop copab cvv eleq1 coeq2 eqeq2d
      anbi12d cla4egv uniexg opabex2g syl 3ad2ant3 adantr wf ccnv cima wral
      cxp wi eqid cnf 3expia ancoms 3adant2 3adant1 anim12d imp opelxpi
      ffvelrn syl2an anandirs r19.21aiva fopab2 sylib wb txuni 3adant3 feq3
      mpbird wrex cab wss ctg ctx txval syl5eq eleq2d ctb txbas eltg3 bitrd
      imaeq2 eleq1d ciun iunopn 3simp3 ad2antrr cin fveq2 opeq12d opex fvopab4
      fvex opelxp syl6bb adantl pm5.32da anandi wfn ffn elpreima 3syl bi2anan9
      elin syl5bb 3bitr4d eqrdv sylan inopn cnima ex 3expa ancom1s 3adantl2
      adantlrr adantrr 3adantl1 adantlrl adantrl syl3anc eqeltrd biimprcd syl6
      r19.23advv ssel2 visset eqeq1 2rexbidv elab sylan2 anassrs sylanc imaiun
      syl5eqel syl5cbir expimpd 19.23adv sylbid r19.21aiv jca iscn txtop 3impa
      c1st cres eqidd wfun cdm fvco wfo fo1st fofn ax-mp ssv fnssres mp2an
      fnfun a1i ffun fdm biimpar fveq2d fvres op1st 3eqtrd eqtr2d eqfnfv crn
      fnco frn reseq2 eqtri coeq1i syl6eqr c2nd fo2nd op2nd jca32 sylc
      imdistani wal immo sylibd anim1d 3anass syl6ibr 19.21aiv upxp eumo 3expb
      3ad2antl3 sylanbrc ) HAGUGZUHZUIZIBUYGUHZUIZUJZGFEUKULZUMZUYGUYMUNZUYLUJZ
      GUOZUYPGUPZCUQUNZDUQUNZFUQUNZURZHFCUKULUNZIFDUKULUNZUJZUJZUYNUYPGUSUYQUYR
      UJUYLGUYMUTUYPGVAVBSUGZFVCZUNZTUGVUGHVDZVUGIVDZVEZUIUJSTVFZVGUNZVUMUYMUNZ
      HAVUMUHZUIZIBVUMUHZUIZUJZUJZUYQVUFUYPVVAGVUMVGUYGVUMUIZUYOVUOUYLVUTUYGVUM
      UYMVHVVBUYIVUQUYKVUSVVBUYHVUPHUYGVUMAVIVJVVBUYJVURIUYGVUMBVIVJVKVKVLVUBVU
      NVUEVUAUYSVUNUYTVUAVUHVGUNZVUNFUQVMZSTVUHVULVGVNVOVPVQVUFVUOVUQVUSVUFVUOV
      UHEVCZVUMVRZVUMVSZUAUGZVTZFUNZUAEWAZUJZVUFVVFVVKVUFVVFVUHJKWBZVUMVRZVUFVU
      HJHVRZVUHKIVRZUJZVVNVUBVUEVVQVUBVUCVVOVUDVVPUYSVUAVUCVVOWCZUYTVUAUYSVVRVU
      AUYSVUCVVOHFCVUHJVUHWDZNWEWFWGWHUYTVUAVUDVVPWCZUYSVUAUYTVVTVUAUYTVUDVVPIF
      DVUHKVVSOWEWFWGWIWJZWKZVVQVULVVMUNZSVUHWAVVNVVQVWCSVUHVVOVVPVUIVWCVUJJUNV
      UKKUNVWCVVOVUIUJVVPVUIUJVUJVUKJKWLVUHJVUGHWMVUHKVUGIWMWNWOWPSTVUHVVMVULVU
      MVUMWDZWQWRZVOVUFVVEVVMUIZVVFVVNWSVUBVWFVUEUYSUYTVWFVUACDEJKMNOWTXAZVQVVE
      VVMVUHVUMXBVOXCVUFVVJUAEVUFVVHEUNZUBUGZUCUGZUDUGZUEUGZWBZUIZUEDXDUDCXDZUC
      XEZXFZVVHVWIVCZUIZUJZUBUOZVVJVUBVWHVXAWSZVUEUYSUYTVXBVUAUYSUYTUJZVWHVVHVW
      PXGVDZUNZVXAVXCEVXDVVHVXCCDXHULVXDEUCUEUDCDXIMXJXKVXCVWPXLUNVXEVXAWSUCUEU
      DCDXMUBVVHVWPXNVOXOXAVQVUFVWTVVJUBVUFVWQVWSVVJVWSVVJVVGVWRVTZFUNVUFVWQUJZ
      VWSVVIVXFFVVHVWRVVGXPXQVXGUFVWIVVGUFUGZVTZXRZFVXFVUAVXIFUNZUFVWIWAVXJFUNV
      XGUFVWIVXIFXSVUBVUAVUEVWQUYSUYTVUAXTZYAVXGVXKUFVWIVUFVWQVXHVWIUNZVXKVUFVX
      HVWMUIZUEDXDUDCXDZVXKVWQVXMUJZVUFVXOVXKVUFVXNVXKUDUECDVUFVWKCUNZVWLDUNZUJ
      ZVVGVWMVTZFUNZVXNVXKWCVUFVXSVYAVUFVXSUJZVXTHVSVWKVTZIVSVWLVTZYBZFVVQVXSVX
      TVYEUIVUFVVQVXSUJZUAVXTVYEVYFVVHVUHUNZVVHVUMVDZVWMUNZUJZVYGVVHHVDZVWKUNZU
      JZVYGVVHIVDZVWLUNZUJZUJZVVHVXTUNZVVHVYEUNZVYFVYJVYGVYLVYOUJZUJVYQVYFVYGVY
      IVYTVYGVYIVYTWSVYFVYGVYIVYKVYNVEZVWMUNVYTVYGVYHWUAVWMSTVVHVULWUAVUHVUMVUG
      VVHUIVUJVYKVUKVYNVUGVVHHYCVUGVVHIYCYDVWDVYKVYNYEYFZXQVYKVYNVWKVWLVVHIYGZY
      HYIYJYKVYGVYLVYOYLYIVVQVYRVYJWSZVXSVVQVVNVUMVUHYMZWUDVWEVUHVVMVUMYNZVUHVV
      HVWMVUMYOYPVQVVQVYSVYQWSVXSVVQVVHVYCUNZVVHVYDUNZUJVYQVYSVVOWUGVYMVVPWUHVY
      PVVOHVUHYMZWUGVYMWSVUHJHYNZVUHVVHVWKHYOVOVVPIVUHYMZWUHVYPWSVUHKIYNZVUHVVH
      VWLIYOVOYQVVHVYCVYDYRYSVQYTUUAVWBUUBVUAVYCFUNZVYDFUNZVYEFUNVYBVYCVYDFUUCV
      UBVUAVUEVXSVXLYAVUFVXQWUMVXRVUBVUCVXQWUMVUDVUBVUCUJVXQWUMUYSVUAVUCVXQWUMW
      CZUYTVUAUYSVUCWUOVUAUYSVUCWUOVUAUYSVUCURVXQWUMVWKHFCUUDUUEUUFUUGUUHWKUUIU
      UJVUFVXRWUNVXQVUBVUDVXRWUNVUCVUBVUDUJVXRWUNUYTVUAVUDVXRWUNWCZUYSVUAUYTVUD
      WUPVUAUYTVUDWUPVUAUYTVUDURVXRWUNVWLIFDUUDUUEUUFUUGUUKWKUULUUMUUNUUOUUEVXN
      VXKVYAVXNVXIVXTFVXHVWMVVGXPXQUUPUUQUURWKVXPVXHVWPUNVXOVWIVWPVXHUUSVWOVXOU
      CVXHUFUUTVWJVXHUIVWNVXNUDUECDVWJVXHVWMUVAUVBUVCWRUVDUVEWPUVFUFVVGVWIUVGUV
      HUVIUVJUVKUVLUVMUVNVUBVUOVVLWSZVUEUYSUYTVUAWUQVUAVXCWUQVUAEUQUNZWUQVXCUAV
      UMFEVUHVVEVVSVVEWDZUVOCDEMUVPZUVDWGUVQVQXCVUFVVQVUQVWBVVQHUVRVVMUVSZVUMUH
      ZVUPVVQHWVBUIZVUHVUHUIZVYKVVHWVBVDZUIZUAVUHWAZUJZVVQWVDWVGVVQVUHUVTZVVQWV
      FUAVUHVVQVYGUJZWVEVYHWVAVDZVYKWVAUWAZVUMUWAZVVHVUMUWBZUNZWVEWVKUIWVJVVHWV
      AVUMUWCWVLWVJWVAVVMYMZWVLUVRVGYMZVVMVGXFZWVPVGVGUVRUWDWVQUWEVGVGUVRUWFUWG
      VVMUWHZVGVVMUVRUWIUWJZVVMWVAUWKUWGUWLWVJVVNWVMVVQVVNVYGVWEVQVUHVVMVUMUWMV
      OZVVQWVOVYGVVQWVNVUHVVHVVQVVNWVNVUHUIVWEVUHVVMVUMUWNVOXKUWOZUUNWVJWVKWUAW
      VAVDZWUAUVRVDZVYKWVJVYHWUAWVAVYGVYHWUAUIVVQWUBYJZUWPWVJWUAVVMUNZWWCWWDUIV
      VOVVPVYGWWFVYKJUNVYNKUNWWFVVOVYGUJVVPVYGUJVYKVYNJKWLVUHJVVHHWMVUHKVVHIWMW
      NWOZWUAVVMUVRUWQVOWWDVYKUIWVJVYKVYNVVHHYGZUWRUWLUWSUWTWPUVNWUIWVBVUHYMZWV
      CWVHWSVVQUAVUHVUHHWVBUXAVVOWUIVVPWUJVQWVPWUEVUMUXBVVMXFZWWIVVQVVMVUHWVAVU
      MUXCWVPVVQWVTUWLVVQVVNWUEVWEWUFVOZVVQVVNWWJVWEVUHVVMVUMUXDVOZUUNUVFXCAWVA
      VUMAUVRLUVSZWVAQLVVMUIZWWMWVAUIPLVVMUVRUXEUWGUXFZUXGUXHVOVUFVVQVUSVWBVVQI
      UXIVVMUVSZVUMUHZVURVVQIWWQUIZWVDVYNVVHWWQVDZUIZUAVUHWAZUJZVVQWVDWXAWVIVVQ
      WWTUAVUHWVJWWSVYHWWPVDZVYNWWPUWAZWVMWVOWWSWXCUIWVJVVHWWPVUMUWCWXDWVJWWPVV
      MYMZWXDUXIVGYMZWVRWXEVGVGUXIUWDWXFUXJVGVGUXIUWFUWGWVSVGVVMUXIUWIUWJZVVMWW
      PUWKUWGUWLWWAWWBUUNWVJWXCWUAWWPVDZWUAUXIVDZVYNWVJVYHWUAWWPWWEUWPWVJWWFWXH
      WXIUIWWGWUAVVMUXIUWQVOWXIVYNUIWVJVYKVYNWWHWUCUXKUWLUWSUWTWPUVNWUKWWQVUHYM
      ZWWRWXBWSVVQUAVUHVUHIWWQUXAVVPWUKVVOWULYJWXEWUEWWJWXJVVQVVMVUHWWPVUMUXCWX
      EVVQWXGUWLWWKWWLUUNUVFXCBWWPVUMBUXILUVSZWWPRWWNWXKWWPUIPLVVMUXIUXEUWGUXFZ
      UXGUXHVOUXLUXMVUFVUBVVQUJZUYRVUBVUEVVQVWAUXNUYPVUHVVMUYGVRZUYIUYKURZWCZGU
      XOWXOGUPZUYRWXMUYPWXOGUXPWXMWXPGWXMUYPWXNUYLUJWXOWXMUYOWXNUYLVUBUYOWXNWCV
      VQVUBUYOVUHVVEUYGVRZWXNUYSUYTVUAUYOWXRWCZVUAVXCWXSVUAWURWXSVXCVUAWURUYOWX
      RUYGFEVUHVVEVVSWUSWEWFWUTUVDWGUVQVUBVWFWXRWXNWSVWGVVEVVMVUHUYGXBVOUXQVQUX
      RWXNUYIUYKUXSUXTUYAVUAUYSVVQWXQUYTVVCVVQWXQVUAVVCVVOVVPWXQVVCVVOVVPURWXOG
      USWXQVUHJKVGABGHIWWOWXLUYBWXOGUYCVOUYDVVDUUBUYEUXMVOUYF $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R h x y $. $d S h x y $. $d T h x y $. $d U h x y $. $d W h x y $.
    $d X h x y $. $d Y h x y $. $d Z h x y $. $d P h x y $. $d Q h x y $.
    $d F h x y $.
    txcn.1 $e |- T = ( R tX S ) $.
    txcn.2 $e |- X = U. R $.
    txcn.3 $e |- Y = U. S $.
    txcn.4 $e |- Z = ( X X. Y ) $.
    txcn.5 $e |- W = U. U $.
    txcn.6 $e |- P = ( 1st |` Z ) $.
    txcn.7 $e |- Q = ( 2nd |` Z ) $.
    $( A map into the product of two topological spaces is continuous iff both
       of its projections are continuous.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    txcn $p |- ( ( ( R e. Top /\ S e. Top /\ U e. Top ) /\ F : W --> Z ) ->
                  ( F e. ( U Cn T ) <-> ( ( P o. F ) e. ( U Cn R ) /\
                                          ( Q o. F ) e. ( U Cn S ) ) ) ) $=
      ( vh vx vy ctop wcel w3a wf wa ccn co ccom wi c1st cres tx1cn syl5eqel
      3adant3 cnco ex 3simp3 txtop 3simp1 syl3anc mpan2d c2nd tx2cn 3simp2
      jcad adantr cv wceq wrex wreu uptx reurex syl cnf 3expia ancoms 3adant2
      3adant1 anim12d cxp cuni eqid sylan2 3impa wb txuni feq3 sylibd ad2antrr
      pm3.27 simpll simplr 3jca adantll ax-mp biimpi eqidd ad2antlr wal weu
      cvv reseq2 eqtri upxp 3expb uniexg sylan 3ad2antl3 adantlr wex feq1
      coeq2 eqeq2d 3anbi123d eu4 pm3.27bi anbi1d equequ1 imbi12d anbi2d eqeq2
      sylan9bb cla42gv visset fex sylancr mpd mp2and exp31 eleq1 biimpd syl8
      com24 mpdd r19.23adv syld impbid ) CUBUCZDUBUCZFUBUCZUDZHKGUEZUFZGFEUGUHZ
      UCZAGUIZFCUGUHUCZBGUIZFDUGUHUCZUFZUUBUUFUUKUJUUCUUBUUFUUHUUJUUBUUFAECUGUH
      ZUCZUUHYSYTUUMUUAYSYTUFZUKKULZUULACDEIJKLMNOUMQUNUOUUAEUBUCZYSUUFUUMUFZUU
      HUJUUBUUAUUPYSUDUUQUUHGAFECUPUQYSYTUUAURZYSYTUUPUUACDELUSZUOZYSYTUUAUTVAV
      BUUBUUFBEDUGUHZUCZUUJYSYTUVBUUAUUNVCKULZUVABCDEIJKLMNOVDRUNUOUUAUUPYTUUFU
      VBUFZUUJUJUUBUUAUUPYTUDUVDUUJGBFEDUPUQUURUUTYSYTUUAVEVAVBVFVGUUDUUKUUGASV
      HZUIZVIZUUIBUVEUIZVIZUFZSUUEVJZUUFUUBUUKUVKUJUUCUUBUUKUVKUUBUUKUFUVJSUUEV
      KUVKABCDEFSUUGUUIIJKLMNOQRVLUVJSUUEVMVNUQVGUUDUUKHIUUGUEZHJUUIUEZUFZUVKUU
      FUJZUUBUUKUVNUJUUCUUBUUHUVLUUJUVMYSUUAUUHUVLUJZYTUUAYSUVPUUAYSUUHUVLUUGFC
      HIPMVOVPVQVRYTUUAUUJUVMUJZYSUUAYTUVQUUAYTUUJUVMUUIFDHJPNVOVPVQVSVTVGUUDUV
      NUVOUUDUVNUFZUVJUUFSUUEUVRUVEUUEUCZHIJWAZUVEUEZUVJUUFUJUUBUVSUWAUJUUCUVNU
      UBUVSHEWBZUVEUEZUWAYSYTUUAUVSUWCUJZUUAUUNUWDUUAUUPUWDUUNUUAUUPUVSUWCUVEFE
      HUWBPUWBWCVOVPUUSWDVQWEUUBUWBUVTVIZUWCUWAWFYSYTUWEUUACDEIJLMNWGUOUWBUVTHU
      VEWHVNWIWJUVRUVJUWAUVSUUFUVRUVJUWAUVEGVIZUVSUUFUJUVRUVJUWAUWFUVRUVJUFUWAU
      FZUWAUVGUVIUDZHUVTGUEZUUGUUGVIZUUIUUIVIZUDZUWFUVJUWAUWHUVRUVJUWAUFUWAUVGU
      VIUVJUWAWKUVGUVIUWAWLUVGUVIUWAWMWNWOUVRUWLUVJUWAUUCUWLUUBUVNUUCUWIUWJUWKU
      UCUWIKUVTVIZUUCUWIWFOKUVTHGWHWPWQUUCUUGWRUUCUUIWRWNWSWJUWGHUVTTVHZUEZUUGA
      UWNUIZVIZUUIBUWNUIZVIZUDZHUVTUAVHZUEZUUGAUXAUIZVIZUUIBUXAUIZVIZUDZUFZUWNU
      XAVIZUJZUAWTTWTZUWHUWLUFZUWFUJZUWGUWTTXAZUXKUVRUXNUVJUWAUUBUVNUXNUUCUUAYS
      UVNUXNYTHXBUCZUVNUXNUUAUXOUVLUVMUXNHIJXBABTUUGUUIAUUOUKUVTULZQUWMUUOUXPVI
      OKUVTUKXCWPXDBUVCVCUVTULZRUWMUVCUXQVIOKUVTVCXCWPXDXEXFUUAFWBXBHFUBXGPUNZX
      HXIXJWJUXNUWTTXKUXKUWTUXGTUAUXIUWOUXBUWQUXDUWSUXFHUVTUWNUXAXLUXIUWPUXCUUG
      UWNUXAAXMXNUXIUWRUXEUUIUWNUXABXMXNXOXPXQVNUVEXBUCGXBUCZUXKUXMUJUWGUXJUXMT
      UAUVEGXBXBUWNUVEVIZUXJUWHUXGUFZUVEUXAVIZUJUXAGVIZUXMUXTUXHUYAUXIUYBUXTUWT
      UWHUXGUXTUWOUWAUWQUVGUWSUVIHUVTUWNUVEXLUXTUWPUVFUUGUWNUVEAXMXNUXTUWRUVHUU
      IUWNUVEBXMXNXOXRTSUAXSXTUYCUYAUXLUYBUWFUYCUXGUWLUWHUYCUXBUWIUXDUWJUXFUWKH
      UVTUXAGXLUYCUXCUUGUUGUXAGAXMXNUYCUXEUUIUUIUXAGBXMXNXOYAUXAGUVEYBXTYCYDSYE
      UVRUXSUVJUWAUUDUXSUVNUUAYSUUCUXSYTUUCUUAUXSUUCUXOUXSUUAHKXBGYFUXRWDVQXIVG
      WJYGYHYIYJUWFUVSUUFUVEGUUEYKYLYMYNYOYPUQYQYOYR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d T x y z $. $d R x y z $. $d S x y z $. $d U x y z $.
    $d W x y z $. $d F x y z $. $d G x y z $. $d H z $.
  txcnopab.1 $e |- T = ( R tX S ) $.
  txcnopab.2 $e |- W = U. U $.
  txcnopab.3 $e |- H = { <. x , y >. | ( x e. W /\
                                    y = <. ( F ` x ) , ( G ` x ) >. ) } $.
  $( A map into the product of two topological spaces is continuous if both
  of its projections are continuous. (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  txcnopab $p |- ( ( ( R e. Top /\ S e. Top /\ U e. Top ) /\
              ( F e. ( U Cn R ) /\ G e. ( U Cn S ) ) ) -> H e. ( U Cn T ) ) $=
    ( vz ctop wcel w3a ccn co wa c1st cuni cxp cres ccom c2nd wf wi eqid cnf
    3expia ancoms 3adant2 3adant1 anim12d imp wceq cv cfv wral eqidd cop wfun
    cdm fvco wfn cvv wss wfo fo1st fofn ax-mp ssv fnssres mp2an fnfun a1i
    opelxpi ffvelrn syl2an anandirs r19.21aiva fopab2 sylib adantr ffun syl
    fdm eleq2d biimpar syl3anc fveq2 opeq12d opex fvopab4 adantl fveq2d fvres
    fvex op1st syl6eq 3eqtrd jca wb eqfnfv crn fnco ffn frn sylanc mpbird
    eleq1d fo2nd op2nd anbi12d biimprcd mpd txcn syldan ) COPZDOPZFOPZQZGFCRSZP
    ZHFDRSZPZTZTZIFERSPZUACUBZDUBZUCZUDZIUEZYDPZUFYMUDZIUEZYFPZTZYIJYKGUGZJYLHU
    GZTZYTYCYHUUCYCYEUUAYGUUBXTYBYEUUAUHZYAYBXTUUDYBXTYEUUAGFCJYKLYKUIZUJUKULUM
    YAYBYGUUBUHZXTYBYAUUFYBYAYGUUBHFDJYLLYLUIZUJUKULUNUOUPZYHUUCYTUHYCUUCYTYHUU
    CYPYEYSYGUUCYOGYDUUCYOGUQZJJUQZNURZYOUSZUUKGUSZUQZNJUTZTZUUCUUJUUOUUCJVAZUU
    CUUNNJUUCUUKJPZTZUULUUKIUSZYNUSZUUMUUKHUSZVBZYNUSZUUMYNVCZIVCZUUKIVDZPZUULU
    VAUQUUSUUKYNIVEUVEUUSYNYMVFZUVEUAVGVFZYMVGVHZUVIVGVGUAVIUVJVJVGVGUAVKVLYMVM
    ZVGYMUAVNVOZYMYNVPVLVQUUSJYMIUGZUVFUUCUVNUURUUCAURZGUSZUVOHUSZVBZYMPZAJUTUV
    NUUCUVSAJUUAUUBUVOJPZUVSUVPYKPUVQYLPUVSUUAUVTTUUBUVTTUVPUVQYKYLVRJYKUVOGVSJ
    YLUVOHVSVTWAWBABJYMUVRIMWCWDZWEJYMIWFWGZUUCUVHUURUUCUVGJUUKUUCUVNUVGJUQUWAJ
    YMIWHWGWIWJZWKUUSUUTUVCYNUURUUTUVCUQUUCABUUKUVRUVCJIUVOUUKUQUVPUUMUVQUVBUVO
    UUKGWLUVOUUKHWLWMMUUMUVBWNWOWPZWQUUSUVDUVCUAUSZUUMUUSUVCYMPZUVDUWEUQUUAUUBU
    URUWFUUMYKPUVBYLPUWFUUAUURTUUBUURTUUMUVBYKYLVRJYKUUKGVSJYLUUKHVSVTWAZUVCYMU
    AWRWGUUMUVBUUKGWSZWTXAXBWBXCYOJVFZGJVFZUUIUUPXDUUCNJJYOGXEUVIIJVFZIXFYMVHZU
    WIUUCYMJYNIXGUVIUUCUVMVQUUCUVNUWKUWAJYMIXHWGZUUCUVNUWLUWAJYMIXIWGZWKUUAUWJU
    UBJYKGXHWEXJXKXLUUCYRHYFUUCYRHUQZUUJUUKYRUSZUVBUQZNJUTZTZUUCUUJUWRUUQUUCUWQ
    NJUUSUWPUUTYQUSZUVCYQUSZUVBYQVCZUVFUVHUWPUWTUQUUSUUKYQIVEUXBUUSYQYMVFZUXBUF
    VGVFZUVKUXCVGVGUFVIUXDXMVGVGUFVKVLUVLVGYMUFVNVOZYMYQVPVLVQUWBUWCWKUUSUUTUVC
    YQUWDWQUUSUXAUVCUFUSZUVBUUSUWFUXAUXFUQUWGUVCYMUFWRWGUUMUVBUWHUUKHWSXNXAXBWB
    XCYRJVFZHJVFZUWOUWSXDUUCNJJYRHXEUXCUWKUWLUXGUUCYMJYQIXGUXCUUCUXEVQUWMUWNWKU
    UBUXHUUAJYLHXHWPXJXKXLXOXPWPXQYCYHUVNYJYTXDYIUUCUVNUUHUWAWGYNYQCDEFIJYKYLYM
    KUUEUUGYMUILYNUIYQUIXRXSXK $.
    $( [2-Sep-2009] $)
  $}

  ${
    $d R w x y z $. $d S w x y z $. $d T w x y z $. $d X w x y z $.
    $d Y w x y z $. $d J w x y z $. $d K w x y z $. $d L w x y z $.
    $d F w x y z $. $d G w x y z $. $d H w $.
    2txcn.1 $e |- T = ( R tX S ) $.
    2txcn.2 $e |- L = ( J tX K ) $.
    2txcn.3 $e |- X = U. R $.
    2txcn.4 $e |- Y = U. S $.
    2txcn.5 $e |- H = { <. <. x , y >. , z >. | ( ( x e. X /\ y e. Y )
                                  /\ z = <. ( F ` x ) , ( G ` y ) >. ) } $.
    $( The product map between two topological product spaces is continuous.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    2txcn $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( J e. Top /\ K e. Top ) /\
            ( F e. ( R Cn J ) /\ G e. ( S Cn K ) ) ) -> H e. ( T Cn L ) ) $=
      ( vw ctop wcel wa ccn co w3a cv cuni c1st cxp cres ccom cfv c2nd cop
      wceq copab wf wfun cdm fvco ffun ad2antrr wfn cvv wss wfo fo1st fofn
      ax-mp ssv fnssres mp2an fnfun a1i fndm eleq2i biimpri adantl syl3anc
      fvres fveq2d eqtr2d ad2antlr fo2nd opeq12d eqid cnf 3adant3r 3adant2r
      3adant1r 3adant3l 3adant2l 3adant1l jca sylan eqeq2d pm5.32da txuni
      3ad2ant1 eleq2d anbi1d bitr4d opabbidv copab2 fveq2 opeq1d opeq2d
      oprabopab eqtri syl5eq simpll 3adant1 simplr txtop 3jca 3adant2 tx1cn
      simprl cnco sylan32c tx2cn simprr txcnopab eqeltrd ) DUAUBZEUAUBZUCZJUAUB
      ZKUAUBZUCZGDJUDUEUBZHEKUDUEUBZUCZUFZITUGZFUHZUBZCUGZYPGUIMNUJZUKZULZUMZYP
      HUNYTUKZULZUMZUOZUPZUCZTCUQZFLUDUEZYOYPYTUBZYSYPUIUMZGUMZYPUNUMZHUMZUOZUP
      ZUCZTCUQZUUJIYOUUSUUITCYOUUSUULUUHUCUUIYOUULUURUUHYOUULUCUUQUUGYSMJUHZGUR
      ZNKUHZHURZUCZUULUUQUUGUPYOUVEUULUCZUUNUUCUUPUUFUVFUUCYPUUAUMZGUMZUUNGUSZU
      UAUSZYPUUAUTZUBZUUCUVHUPUVFYPGUUAVAUVBUVIUVDUULMUVAGVBVCUVJUVFUUAYTVDZUVJ
      UIVEVDZYTVEVFZUVMVEVEUIVGUVNVHVEVEUIVIVJYTVKZVEYTUIVLVMZYTUUAVNVJVOUULUVL
      UVEUVLUULUVKYTYPUVMUVKYTUPUVQYTUUAVPVJVQVRVSVTUVFUVGUUMGUULUVGUUMUPUVEYPY
      TUIWAVSWBWCUVFUUFYPUUDUMZHUMZUUPHUSZUUDUSZYPUUDUTZUBZUUFUVSUPUVFYPHUUDVAU
      VDUVTUVBUULNUVCHVBWDUWAUVFUUDYTVDZUWAUNVEVDZUVOUWDVEVEUNVGUWEWEVEVEUNVIVJ
      UVPVEYTUNVLVMZYTUUDVNVJVOUULUWCUVEUWCUULUWBYTYPUWDUWBYTUPUWFYTUUDVPVJVQVR
      VSVTUVFUVRUUOHUULUVRUUOUPUVEYPYTUNWAVSWBWCWFYOUVBUVDYFYKYNUVBYGYFYIYNUVBY
      JYFYIYLUVBYMGDJMUVAQUVAWGWHWIWJWKYGYKYNUVDYFYGYJYNUVDYIYGYJYMUVDYLHEKNUVC
      RUVCWGWHWLWMWNWOWPWQWRYOYRUULUUHYOYQYTYPYHYKYQYTUPYNDEFMNOQRWSWTXAXBXCXDI
      AUGZMUBBUGZNUBUCYSUWGGUMZUWHHUMZUOZUPZUCABCXEUUTSUWLYSUUNUWJUOZUPUURABCTM
      NUWGUUMUPZUWKUWMYSUWNUWIUUNUWJUWGUUMGXFXGWQUWHUUOUPZUWMUUQYSUWOUWJUUPUUNU
      WHUUOHXFXHWQXIXJXKYIYJFUAUBZUFUUBFJUDUEUBZUUEFKUDUEUBZUUJUUKUBYOYOYIYJUWP
      YKYNYIYHYIYJYNXLXMZYKYNYJYHYIYJYNXNXMZYHYKUWPYNDEFOXOWTZXPUWPYFYIUFUUAFDU
      DUEUBZYLUWQYOYOUWPYFYIUXAYHYNYFYKYFYGYNXLXQUWSXPYHYKUXBYNDEFMNYTOQRYTWGZX
      RWTYKYNYLYHYKYLYMXSXMUUAGFDJXTYAUWPYGYJUFUUDFEUDUEUBZYMUWRYOYOUWPYGYJUXAY
      HYNYGYKYFYGYNXNXQUWTXPYHYKUXDYNDEFMNYTOQRUXCYBWTYKYNYMYHYKYLYMYCXMUUDHFEK
      XTYATCJKLFUUBUUEUUJYQPYQWGUUJWGYDYAYE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A a x y  $. $d J a x y $. $d X a x y $.
    ttcnlem.1 $e |- X = U. J $.
    ttcnlem.2 $e |- F = { <. x , y >. | ( x e. X /\ y = <. x , A >. ) } $.
    $( A special case of ~ txcnopab , when one function is constant.
       Bourbaki TG I.26 prop. 4. $)
    ttcn $p |- ( ( J e. Top /\ A e. X ) -> F e. ( J Cn ( J tX J ) ) ) $=
      ( va ctop wcel wa cv cop wceq copab ctx co ccn wi eleq1 anbi2d opeq2
      eqeq2d opabbidv eleq1d imbi12d w3a cid cres csn cxp pm3.26 3jca idcn
      adantr wf pm3.27 visset fconst jctir cnconst sylan31c eqid cfv fvresi
      eqcomd fvconst2 opeq12d pm5.32i opabbii txcnopab sylan32c vtoclg anabsi7
      syl5eqel ) EJKZCFKZLZAMZFKZBMZVTCNZOZLZABPZEEEQRZSRZDVQVRWFWHKZVQIMZFKZLZ
      WAWBVTWJNZOZLZABPZWHKZTVSWITICFWJCOZWLVSWQWIWRWKVRVQWJCFUAUBWRWPWFWHWRWOW
      EABWRWNWDWAWRWMWCWBWJCVTUCUDUBUEUFUGVQVQVQUHUIFUJZEESRZKZFWJUKZULZWTKZWQW
      LWLVQVQVQVQWKUMZXEXEUNVQXAWKEFGUOUPVQVQWKFXBXCUQZLXDWLXEXEWLWKXFVQWKURFWJ
      IUSZUTVAWJXCEEFFGGVBVCABEEWGEWSXCWPFWGVDGWOWAWBVTWSVEZVTXCVEZNZOZLABWAWNX
      KWAWMXJWBWAVTXHWJXIWAXHVTFVTVFVGWAXIWJFWJVTXGVHVGVIUDVJVKVLVMVNVOHVP $.
      $( [21-Jun-2010] $) $( [21-Jun-2010] $)
  $}


