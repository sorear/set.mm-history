$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Initial and final topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d i o t x y I $.  $d i t x y A $.  $d i o t x y X $.  $d o t x y F $.
    $d i o t x y K $.  $d i t y B $.
    intopcoacon.1 $e |- J = ( topGen ` ( fi ` { x | E. i e. I
        E. o e. K x = ( `' F " o ) } ) ) $.
    ${
      $d o J $.
      $( The underlying set of the initial topology is the domain of the
         mappings ` F ` .  (Contributed by FL, 24-Apr-2012.) $)
      intopcoaconlem3b $p |-
        ( ( ( I e. A /\ X e. B ) /\ I =/= (/) /\
          A. i e. I ( K e. Top /\ F : X --> U. K ) )
             -> U. J = X ) $=
        ( vt wcel wa c0 wne ctop cuni wf wral w3a cv ccnv cima wceq wrex cab
        cfi cfv ctg unieqi wss cvv simp1l abrexexg adantr ralimi 3ad2ant3 jca
        abrexex2g eqid elsubops 3syl wi topopn fimacnv eqcomd anim12i a1i
        imaeq2 eqeq2d rcla4ev syl6 reximdva0 ancoms r19.35 sylib expcom 3imp
        wb simp1r eqeq1 rexbidv elabg syl mpbird sseldd ctb fibas tg1 mpan
        syl11anc fiuni ax-17 visset elabf r19.29 cdm fdm cnvimass sseq1 mpbiri
        r19.23aivw sseq2 syl5ibr adantl imp ex com12 sylbi impcom r19.21aiva
        unissb sylibr sstr2 syl5com imbi1d eqcoms pm2.43i syl5 r19.21aiv
        ssunieq syl5eq ) GBMZJCMZNZGOPZIQMZJIRZFSZNZDGTZUAZHRAUBZFUCZEUBZUDZUEZ
        EIUFZDGUFZAUGZUHUIZUJUIZRZJHUUCKUKYMJUUCMZLUBZJULZLUUCTZUUDJUEYMUUAUUCJ
        YMYDYSAUGUMMZDGTZNUUAUMMZUUAUUCULYMYDUUJYDYEYGYLUNZYLYFUUJYGYKUUIDGYHUU
        IYJEAIYQQUOUPUQURZUSYSDAGBUMUTZUMUUAUUCUUCVAVBVCYMJUUAMZJYQUEZEIUFZDGUF
        ZYFYGYLUURYGYFYLUURVDZYGYFNYKUUQVDZDGUFZUUSYFYGUVAYFUUTDGYFDUBGMNZYKYII
        MZJYOYIUDZUEZNZUUQYKUVFVDUVBYHUVCYJUVEIYIYIVAVEYJUVDJJYIFVFVGVHVIUUPUVE
        EYIIYPYIUEYQUVDJYPYIYOVJVKVLVMVNVOYKUUQDGVPVQVRVSYMYEUUOUURVTYDYEYGYLWA
        YTUURAJCYNJUEZYSUUQDGUVGYRUUPEIYNJYQWBWCWCWDWEWFWGYMUUGLUUCUUFUUCMZUUFU
        UBRZULZYMUUGUUBWHMUVHUVJUUAWIUUFUUBWJWKYMUVJUUGVDZYMUUKUUARZUVIUEYMUVKV
        DZYMYDUUJUUKUULUUMUUNWLUUAUMWMUVMUVIUVLYMUVKUVIUVLUEZUUFUVLULZUUGVDYMUV
        LJULZUVOUUGYMUUGLUUATUVPYMUUGLUUAUUFUUAMZYMUUGUVQUUFYQUEZEIUFZDGUFZYMUU
        GVDYTUVTAUUFUVTAWNLWOYNUUFUEZYSUVSDGUWAYRUVREIYNUUFYQWBWCWCWPYMUVTUUGYL
        YFUVTUUGVDYGYLUVTUUGYLUVTNYKUVSNZDGUFUUGYKUVSDGWQUWBUUGDGYKUVSUUGYJUVSU
        UGVDZYHYJJFWRZUEZUWCYJUWDJJYIFWSVGUVSUUGUWEUUFUWDULZUVRUWFEIUVRUWFYQUWD
        ULFYPWTUUFYQUWDXAXBXCJUWDUUFXDXEWEXFXGXCWEXHURXIXJXKXLLUUAJXMXNUUFUVLJX
        OXPUVNUVJUVOUUGUVIUVLUUFXDXQXEXRVCXSXTYAUUEUUHNJUUDLJUUCYBVGWLYC $.
        $( [25-Nov-2013] $) $( [24-Apr-2012] $)
    $}

    intopcoacon.2 $e |- I e. A $.
    intopcoacon.3 $e |- X e. B $.
    intopcoacon.4 $e |- A. i e. I ( K e. Top /\ F : X --> U. K ) $.
    intopcoacon.5 $e |- I =/= (/) $.
    ${
      $d o J $.
      $( The underlying set of the initial topology is the domain of the
         mappings ` F ` .  (Contributed by FL, 21-Apr-2012.) $)
      intopcoaconlem3 $p |- U. J = X $=
        ( vy cuni wcel cv wss wral wceq ccnv cima wrex cab cvv elisseti ctop
        wf wa rspec simpld abrexexg syl rgen abrexex2g mp2an elsubops ax-mp c0
        wne wi r19.2zb mpbi simprd eqid topopn fimacnv eqcomd imaeq2 eqeq2d
        rcla4ev expcom sylc mprg eqeq1 rexbidv elab mpbir sselii cfi cfv ctg
        ctb fibas fiuni tg1 sseq2 unissb ax-17 visset elabf cdm fdm cnvimass
        sseq1 mpbiri r19.23aivw syl5ibr eqcoms r19.23aiv sylbi mprgbir sstr2
        mpi syl6bi mpsyl mpan eleq2s ssunieq eqcomi ) JHQZJHRESZJTZEHUAJXMUBASZ
        FUCZXNUDZUBZEIUEZDGUEZAUFZHJYBUGRZYBHTGUGRXTAUFUGRZDGUAZYCGBLUHYDDGDSGR
        ZIUIRZYDYFYGJIQZFUJZYGYIUKDGNULZUMZEAIXRUIUNUOUPZXTDAGUGUGUQURUGYBHKUSU
        TJYBRJXRUBZEIUEZDGUEZYNYODGGVAVBYNDGUAYOVCOYNDGVDVEYFYIYHIRZYNYFYGYIYJV
        FZYFYGYPYKIYHYHVGVHUOYIJXQYHUDZUBZYPYNVCYIYRJJYHFVIVJYPYSYNYMYSEYHIXNYH
        UBXRYRJXNYHXQVKVLVMVNUOVOVPYAYOAJJCMUHXPJUBZXTYNDGYTXSYMEIXPJXRVQVRVRVS
        VTWAXOEHXOXNYBWBWCZWDWCZHUUAWERZXNUUBRZXOYBWFYBQZUUAQZUBZUUCUUDUKXNUUFT
        ZXOYCUUGGBRYEYCLYLXTDAGBUGUQURYBUGWGUTXNUUAWHUUHXOVCUUFUUEUUFUUEUBUUHXN
        UUETZXOUUFUUEXNWIUUIUUEJTZXOUUJPSZJTZPYBPYBJWJUUKYBRUUKXRUBZEIUEZDGUEZU
        ULYAUUOAUUKUUOAWKPWLXPUUKUBZXTUUNDGUUPXSUUMEIXPUUKXRVQVRVRWMUUNUULDGYFY
        IUUNUULVCZYQYIFWNZJUBUUQJYHFWOUUQJUURUUNUULJUURUBUUKUURTZUUMUUSEIUUMUUS
        XRUURTFXNWPUUKXRUURWQWRWSJUURUUKWIWTXAUOUOXBXCXDXNUUEJXEXFXGXAXHXIKXJUP
        EJHXKURXL $.
        $( [25-Nov-2013] $) $( [21-Apr-2012] $)
    $}

    $( The initial topology is the coarsest one making the functions ` F `
       continuous .  (Contributed by FL, 14-May-2012.) $)
    intopcoaconb $p |- J = |^| { t e. Top | A. i e. I F e. ( t Cn K ) } $=
      ( vy cv ccnv cima wceq wrex cab cfi cfv ctg ctop wcel ccn co wral wa
      cint crab cvv fvex eleq1 ax-17 hbre1 hbab hbfv hbeq opreq1 eleq2d ralbid
      anbi12d ctb fibas tgcl ax-mp cuni wf wb simpl ralimi rspec eqid iscn
      sylancr simprd imaeq2 eqeq2d cbvrexf rexbii abbii fveq2i intopcoaconlem3
      a1i feq2d mpbird wex wi wal w3a ra42e biimpi reximi syl 3expia 19.21aiv
      wfn ffn fnex cnvexg sylancl adantl imaexg alexeqd bicomd adantr clelab
      sylibr ex wss abrexexg rgen abrexex2g mp2an elsubops sseli syl6
      r19.21aiv mpbir2and pm3.2i r19.29 eleq1a imp r19.23aivw syl6bi com4l
      imp3a com3r abss sbincgt syldan eqint df-rab inteqi 3eqtr4i ) ARZGSZFRZTZ
      UAZFJUBZEHUBZAUCZUDUEZUFUEZBRZUGUHZGUUJJUIUJZUHZEHUKZULZBUCZUMZIUUNBUGUNZ
      UMUUIUOUHUUIUUQUAUUHUFUPUUOUUIUGUHZGUUIJUIUJZUHZEHUKZULBUUIUOUUJUUIUAZUUK
      UUSUUNUVBUUJUUIUGUQUVCUUMUVAEHEQQUUJUUIQRZUUJUHEUREQUUHUFUVDUFUHEUREQUUGU
      DUVDUDUHEURUUFEAQUUEEHUSUTVAVAVBUVCUULUUTGUUJUUIJUIVCVDVEVFUUSUVBUUHVGUHU
      USUUGVHUUHVIVJZUVAEHERHUHZUVAUUIVKZJVKZGVLZUUAUUJTZUUIUHZBJUKZUVFUUSJUGUH
      ZUVAUVIUVLULVMUVEUVMEHUVMKUVHGVLZULZEHUKZUVMEHUKOUVOUVMEHUVMUVNVNVOVJVPZB
      GUUIJUVGUVHUVGVQUVHVQZVRVSUVFUVIUVNUVFUVMUVNUVOEHOVPVTUVFUVGKUVHGUVGKUAUV
      FACDEBGHUUIJKUUHYTUVJUAZBJUBZEHUBZAUCZUDUEUFUUGUWBUDUUFUWAAUUEUVTEHUUDUVS
      FBQJUVDJUHZFURZUWCBURZUUDBURZUVSFURZUUBUUJUAUUCUVJYTUUBUUJUUAWAWBWCWDWEWF
      WFMNOPWGWHWIWJUVFUVKBJUVFUUJJUHZUVJUUGUHZUVKUVFUWHUWIUVFUWHULZUVSUUFULAWK
      ZUWIUWJUWKUVSUUFWLZAWMZUWJUWLAUVFUWHUVSUUFUVFUWHUVSWNUWAUUFUVSEBHJWOUVTUU
      EEHUVTUUEUVSUUDBFQJUWEUWDUWGUWFUUJUUBUAUVJUUCYTUUJUUBUUAWAWBWCWPWQWRWSWTU
      VFUWKUWMVMZUWHUVFUVJUOUHZUWNUVFUUAUOUHZUWOUWPEHUVPUWPEHUKOUVOUWPEHUVNUWPU
      VMUVNGKXAZKDUHZUWPKUVHGXBNUWQUWRULGUOUHUWPKDGXCGUOXDWRXEXFVOVJVPUUAUUJUOX
      GWRUWOUWMUWKUUFAUVJUOXHXIWRXJWJUUFAUVJXKXLXMUUGUUIUVJUUGUOUHZUUGUUIXNHCUH
      UUEAUCUOUHZEHUKUWSMUWTEHUVFUVMUWTUVQFAJUUCUGXOWRXPUUEEAHCUOXQXRUOUUGUUIUU
      IVQXSVJXTYAYBYCXPYDUUKUUNUUGUUJXNZUUIUUJXNUUOUUFYTUUJUHZWLZAWMUXAUUOUXCAU
      UKUUNUXCUUNUUFUUKUXBUUNUUFUUKUXBWLZUUNUUFULUVPUUMUUEULZEHUBZUXDOUUMUUEEHY
      EUVPUXFULUVOUXEULZEHUBUXDUVOUXEEHYEUXGUXDEHUVOUXEUXDUVMUXEUXDWLUVNUVMUUMU
      UEUXDUUKUVMUUMUUEUXBUUKUVMUUMUUEUXBWLZWLUUKUVMULUUMUUJVKZUVHGVLZUUCUUJUHZ
      FJUKZULUXHFGUUJJUXIUVHUXIVQUVRVRUXLUXHUXJUXLUUEUXBUXLUUEULUXKUUDULZFJUBUX
      BUXKUUDFJYEUXMUXBFJUXKUUDUXBUUCUUJYTYFYGYHWRXMXFYIXMYJYKXJYGYHWRVSXMYLYGW
      TUUFAUUJYMXLUUGUUJYNYOYPVJLUURUUPUUNBUGYQYRYS $.
      $( [26-Nov-2013] $) $( [14-May-2012] $)

    $d o J $.
    $( The initial topology makes the functions ` F ` continuous.  (Contributed
       by FL, 15-Oct-2012.)  (Revised by Mario Carneiro, 25-Nov-2013.) $)
    intopcoaconc $p |- A. i e. I F e. ( J Cn K ) $=
      ( vy ccn co wcel cv cuni wf ccnv cima wral ctop wa wb wceq wrex cab cfi
      cfv ctg ctb fibas tgcl ax-mp eqeltri simpl ralimi rspec c0 wne pm3.2i
      intopcoaconlem3b mp3an eqcomi eqid iscn sylancr simprd crab cint wi ra4
      com12 ad2antrr simprl simprr simplr cnima syl31anc expr syld impr
      r19.21aiva cvv wfn ffn adantl fnex sylancl cnvexg syl imaexg elintrabg
      3syl adantr mpbird intopcoaconb syl6eleqr mpbir2and rgen ) FHIQRSZDGDTGSZ
      XEJIUAZFUBZFUCZETZUDZHSZEIUEZXFHUFSIUFSZXEXHXMUGUHHATXKUIEIUJDGUJAUKZULUM
      ZUNUMZUFKXPUOSXQUFSXOUPXPUQURUSXNDGXNXHUGZDGUEZXNDGUENXRXNDGXNXHUTVAURVBZ
      EFHIJXGHUAZJGBSZJCSZUGGVCVDXSYAJUIYBYCLMVEONABCDEFGHIJKVFVGVHXGVIVJVKXFXN
      XHXRDGNVBZVLXFXLEIXFXJISZUGZXKFPTZIQRSZDGUEZPUFVMVNZHYFXKYJSZYIXKYGSZVOZP
      UFUEZYFYMPUFYFYGUFSZYIYLYFYOYIYLYFYOUGYIYHYLXFYIYHVOYEYOYIXFYHYHDGVPVQVRY
      FYOYHYLYFYOYHUGZUGYOXNYHYEYLYFYOYHVSXFXNYEYPXTVRYFYOYHVTXFYEYPWAXJFYGIWBW
      CWDWEWFWDWGXFYKYNUHZYEXFXRXKWHSZYQYDXRXIWHSZYRXRFWHSZYSXRFJWIZYCYTXHUUAXN
      JXGFWJWKMJCFWLWMFWHWNWOXIXJWHWPWOYIPXKUFWHWQWRWSWTAPBCDEFGHIJKLMNOXAXBWGX
      CXD $.
      $( [26-Nov-2013] $) $( [15-Oct-2012] $)
  $}

  ${
    $d X x y z $.  $d J x y z $.  $d R x y z $.
    qusp.1 $e |- X = U. J $.
    qusp.2 $e |- Er R $.
    $( A quotient space is a topology.  (Contributed by FL, 4-Jun-2007.) $)
    qusp $p |- ( J e. Top ->
      { x | ( x C_ ( X /. R ) /\ U. x e. J ) } e. Top ) $=
      ( vy vz ctop wcel cv cqs wss cuni wa cab wi wal cin wral df-ral r19.26
      unissb simplr wceq wex uniuni simpll hba1 ax-17 eleq1a imim2i com23
      imp3a a4s 19.23ad 19.21aiv sylbi adantl abss sylibr uniopn syl11anc
      syl5eqel jca exp31 syl5bir syl5bi ssab visset uniex sseq1 unieq eleq1d
      anbi12d elab 3imtr4g elabf an4 ssinss1 ad2antrr uninqs ad2antrl inopn
      3expib com12 impcom eqeltrd ex expdimp inex1 r19.21aiv cvv wb cpw elpw
      bicomi a1i anbi1d abbidv ssab2 uniexg qsexg pwexg 3syl ssexg syldan mpan
      istopg syl mpbird ) CIJZAKZDBLZMZXMNZCJZOZAPZIJZGKZXSMZYANZXSJZQZGRZYAHKZ
      SZXSJZHXSTZGXSTZOZXLYFYKXLYEGXLXMYAJZXRQARZYCXNMZYCNZCJZOZYBYDYNXRAYATZXL
      YRXRAYAUAYSXOAYATZXQAYATZOXLYRXOXQAYAUBXLYTUUAYRYTYOXLUUAYRQAYAXNUCXLYOUU
      AYRXLYOOZUUAOZYOYQXLYOUUAUDUUCYPYGXPUEZYMOZAUFZHPZNZCHAYAUGUUCXLUUGCMZUUH
      CJXLYOUUAUHUUCUUFYGCJZQZHRZUUIUUAUULUUBUUAYMXQQZARZUULXQAYAUAUUNUUKHUUNUU
      EUUJAUUMAUIUUJAUJUUMUUEUUJQAUUMUUDYMUUJUUMYMUUDUUJXQUUDUUJQYMXPCYGUKULUMU
      NUOUPUQURUSUUFHCUTVAUUGCVBVCVDVEVFVGUNVHVGXRAYAVIXRYRAYCYAGVJZVKXMYCUEZXO
      YOXQYQXMYCXNVLUUPXPYPCXMYCVMVNVOVPVQUQXLYAXSJZYJQZGRYKXLUURGUUQYAXNMZYCCJ
      ZOZXLYJXRUVAAYAUVAAUJUUOXMYAUEZXOUUSXQUUTXMYAXNVLUVBXPYCCXMYAVMVNVOVRXLUV
      AYJXLUVAOZYIHXSUVCYGXNMZYGNZCJZOZYHXNMZYHNZCJZOZYGXSJYIXLUVAUVGUVKUVAUVGO
      UUSUVDOZUUTUVFOZOZXLUVKUUSUUTUVDUVFVSXLUVNUVKXLUVNOZUVHUVJUVNUVHXLUUSUVHU
      VDUVMYAYGXNVTWAUSUVOUVIYCUVESZCUVLUVIUVPUEXLUVMDYAYGBFWBWCUVNXLUVPCJZUVMX
      LUVQQUVLXLUVMUVQXLUUTUVFUVQYCUVECWDWEWFUSWGWHVEWIVHWJXRUVGAYGUVGAUJHVJXMY
      GUEZXOUVDXQUVFXMYGXNVLUVRXPUVECXMYGVMVNVOVRXRUVKAYHUVKAUJYAYGUUOWKXMYHUEZ
      XOUVHXQUVJXMYHXNVLUVSXPUVICXMYHVMVNVOVRVQWLWIVHUQYJGXSUAVAVEXLXSWMJXTYLWN
      XLXSXMXNWOZJZXQOZAPZWMXLXRUWBAXLXOUWAXQXOUWAWNXLUWAXOXMXNAVJWPWQWRWSWTUWC
      UVTMZXLUWCWMJZXQAUVTXAUWDXLUVTWMJZUWEUWDXLODWMJZXNWMJUWFXLUWGUWDXLDCNWMEC
      IXBVDUSDBXCXNWMXDXEUWCUVTWMXFXGXHWHGHWMXSXIXJXK $.
      $( [4-Jun-2007] $)
  $}

  ${
    $d A x $.  $d J u v x z $.  $d K u v x z $.
    eltpt.1 $e |- B = { z | E. u e. J E. v e. K z = ( u X. v ) } $.
    $( Condition for a set to belong to a product topology..  (Contributed by
       FL, 18-Apr-2010.) $)
    eltpt $p |- ( ( J e. Top /\ K e. Top ) -> ( A e. ( J tX K ) <-> E. x
       ( x C_ B /\ A = U. x ) ) ) $=
      ( ctop wcel wa ctx co cv cxp wceq wrex cab ctg cfv wss cuni wex txval
      eleq2d ctb wb txbas eltg3 syl sseq2i bicomi anbi1i exbii a1i 3bitrd ) GJK
      HJKLZEGHMNZKEBODOCOPQCHRDGRBSZTUAZKZAOZUTUBZEVCUCQZLZAUDZVCFUBZVELZAUDZUR
      USVAEBCDGHUEUFURUTUGKVBVGUHBCDGHUIAEUTUJUKVGVJUHURVFVIAVDVHVEVHVDFUTVCIUL
      UMUNUOUPUQ $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d A a x y $.  $d J a x y $.  $d X a x y $.
    ttcnlem.1 $e |- X = U. J $.
    ttcnlem.2 $e |- F = { <. x , y >. | ( x e. X /\ y = <. x , A >. ) } $.
    $( A special case of ~ txcnopab , when one projection is constant and the
       other the identity.  Bourbaki TG I.26 prop. 4.  (Contributed by FL,
       21-Jun-2010.) $)
    ttcn $p |- ( ( J e. Top /\ A e. X ) -> F e. ( J Cn ( J tX J ) ) ) $=
      ( va ctop wcel wa cv cop wceq copab ctx co ccn wi eleq1 anbi2d opeq2
      eqeq2d opabbidv eleq1d imbi12d cid cres csn cxp simpl idcn adantr wf
      simpr visset fconst jctir cnconst syl21anc eqid cfv fvresi eqcomd
      fvconst2 opeq12d pm5.32i opabbii txcnopab syl32anc vtoclg anabsi7
      syl5eqel ) EJKZCFKZLZDAMZFKZBMZVRCNZOZLZABPZEEEQRZSRZHVOVPWDWFKZVOIMZFKZL
      ZVSVTVRWHNZOZLZABPZWFKZTVQWGTICFWHCOZWJVQWOWGWPWIVPVOWHCFUAUBWPWNWDWFWPWM
      WCABWPWLWBVSWPWKWAVTWHCVRUCUDUBUEUFUGWJVOVOVOUHFUIZEESRZKZFWHUJZUKZWRKZWO
      VOWIULZXCXCVOWSWIEFGUMUNWJVOVOWIFWTXAUOZLXBXCXCWJWIXDVOWIUPFWHIUQZURUSWHX
      AEEFFGGUTVAABEEWEEWQXAWNFWEVBGWMVSVTVRWQVCZVRXAVCZNZOZLABVSWLXIVSWKXHVTVS
      VRXFWHXGVSXFVRFVRVDVEVSXGWHFWHVRXEVFVEVGUDVHVIVJVKVLVMVN $.
      $( [21-Jun-2010] $)
  $}

  ${
    $d A a x y $.  $d J a x y $.  $d X a x y $.
    ttcn2lem.1 $e |- X = U. J $.
    ttcn2lem.2 $e |- F = { <. x , y >. | ( x e. X /\ y = <. A , x >. ) } $.
    $( A special case of ~ txcnopab , when one projection is constant and the
       other the identity.  Bourbaki TG I.26 prop. 4.  (Contributed by FL,
       10-Jul-2012.) $)
    ttcn2 $p |- ( ( J e. Top /\ A e. X ) -> F e. ( J Cn ( J tX J ) ) ) $=
      ( va ctop wcel wa cv cop wceq copab ctx co ccn wi eleq1 ax-17 opeq1
      eqeq2d anbi2d opabbid eleq1d imbi2d imbi12d csn cxp cid cres simpl wf
      simpr visset fconst jctir cnconst syl21anc idcn adantr eqid cfv cvv
      fvconst2g eqcomd mpan fvresi opeq12d pm5.32i opabbii txcnopab syl32anc
      expcom vtoclg pm2.43i impcom syl5eqel ) EJKZCFKZLDAMZFKZBMZCWCNZOZLZABPZE
      EEQRZSRZHWBWAWIWKKZWBWAWLTZIMZFKZWAWDWEWNWCNZOZLZABPZWKKZTZTWBWMTICFWNCOZ
      WOWBXAWMWNCFUAXBWTWLWAXBWSWIWKXBWRWHABXBAUBXBBUBXBWQWGWDXBWPWFWEWNCWCUCUD
      UEUFUGUHUIWAWOWTWAWOLZWAWAWAFWNUJZUKZEESRZKZULFUMZXFKZWTWAWOUNZXJXJXCWAWA
      WOFXDXEUOZLXGXJXJXCWOXKWAWOUPFWNIUQZURUSWNXEEEFFGGUTVAWAXIWOEFGVBVCABEEWJ
      EXEXHWSFWJVDGWRWDWEWCXEVEZWCXHVEZNZOZLABWDWQXPWDWPXOWEWDWNXMWCXNWNVFKZWDW
      NXMOXLXQWDLXMWNFWNWCVFVGVHVIWDXNWCFWCVJVHVKUDVLVMVNVOVPVQVRVSVT $.
      $( [17-Jul-2012] $) $( [10-Jul-2012] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.  $d L x $.
    $( If ` F ` is continous over two topologies ` J ` and ` K ` then it is
       continuous over ` ( J i^i K ) ` .  (Contributed by FL, 27-Nov-2011.) $)
    intcont $p |- ( ( ( J e. Top /\ K e. Top /\ L e. Top ) /\ U. J = U. K
    /\ ( F e. ( J Cn L ) /\ F e. ( K Cn L ) ) )
    -> F e. ( ( J i^i K ) Cn L ) ) $=
      ( vx ctop wcel w3a cuni wceq ccn co wa cin wf ccnv cv cima wral wb eqid
      iscn 3adant2 adantr 3adant1 anbi12d biimp3a wi pm3.2an3 imp 3adant3
      unint2t feq2 biimpd eqcoms 3syl adantrd elin simplbi2 ral2imi a1i
      anim12d com12 an4s mpcom inttop4 3ad2ant1 simp13 syl11anc mpbird ) BFGZCF
      GZDFGZHZBIZCIZJZABDKLGZACDKLGZMZHZABCNZDKLGZWBIZDIZAOZAPEQRZWBGZEDSZMZVOW
      EAOZWGBGZEDSZMZVPWEAOZWGCGZEDSZMZMZWAWJVNVQVTWSVNVQMVRWNVSWRVNVRWNTZVQVKV
      MWTVLEABDVOWEVOUAWEUAZUBUCUDVNVSWRTZVQVLVMXBVKEACDVPWEVPUAXAUBUEUDUFUGWKW
      OWMWQWAWJUHWAWKWOMZWMWQMZMWJWAXCWFXDWIWAWKWFWOWAVKVLVQHZWDVOJWKWFUHZVNVQX
      EVTVNVQXEVKVLVQXEUHZVMVKVLXGVKVLVQUIUJUKUJUKBCULXFVOWDVOWDJWKWFVOWDWEAUMU
      NUOUPUQXDWIUHWAWMWQWIWLWPWHEDWHWLWPWGBCURUSUTUJVAVBVCVDVEWAWBFGZVMWCWJTVN
      VQXHVTVKVLXHVMBCVFUKVGVKVLVMVQVTVHEAWBDWDWEWDUAXAUBVIVJ $.
      $( [27-Nov-2011] $)
  $}

  $c topX $.

  $( Extend class notation with a function whose value is a product
     topology. $)
  ctopx $a class topX $.

  ${
    $d f y t x i $.
    $( Note: does the intersection gives us the coarsest topology? Yes by
       iinss and inttop3 . It works with an empty family. The topology is
       { (/) } . $)
    $( The product topology of a family ` f ` of topologies is the coarsest
       topology over the product of the underlying sets that makes the
       projections continuous.  (Bourbaki TG I.14 ex. 3) Experimental. $)
    df-prtop $a |- topX = { <. f , y >. | ( f : dom f --> Top /\
       y = |^| { t e. Top | (
       U. t = X_ x e. dom f U. ( f ` x ) /\ A. i e. dom f
         ( X_ x e. dom f U. ( f ` x ) pr i ) e. ( t Cn ( f ` i ) ) ) } ) } $.
  $}

  ${
    $d A i $.  $d F i t x $.  $d I i t x $.
    $( Lemma for ~ usptop . $)
    usptoplem $p |- ( ( F : I --> Top /\ I e. A )
                      -> ~P X_ x e. I U. ( F ` x )
                         e. { t
                            e. Top
                            | ( U. t
                                  = X_ x e. I U. ( F ` x )
                               /\ A. i
                                  e. I
                                        ( X_ x e. I U. ( F ` x )
                                          pr
                                          i )
                                     e. ( t Cn ( F ` i ) ) ) } ) $=
      ( ctop wf wcel wa cv cfv cuni cixp cpw wceq cpro co ccn wral crab cvv
      fvex uniex a1i rgen jctr adantl ixpexg distopg 3syl unipw ffvelrn
      adantlr simpr sylancl eqid weq fveq2 unieqd prmapcp2 sylan simplr
      mapdiscn syl111anc r19.21aiva jca32 unieq eqeq1d opreq1 eleq2d ralbidv
      anbi12d elrab sylibr ) FGEHZFCIZJZAFAKZELZMZNZOZGIZWCMZWBPZWBDKZQRZWCWGEL
      ZSRZIZDFTZJZJWCBKZMZWBPZWHWNWISRZIZDFTZJZBGUAIVRWDWFWLVRVQWAUBIZAFTZJZWBU
      BIZWDVQXCVPVQXBXAAFXAVSFIVTVSEUCUDUEUFZUGUHAFWACUBUIZWBUBUJUKWFVRWBULUEVR
      WKDFVRWGFIZJZWIGIZWBWIMZWHHZXDWKVPXGXIVQFGWGEUMUNVRXDXGXKVRVQXBXDVPVQUOXE
      XFUPAFWAXJWBUBWGWBUQADURVTWIVSWGEUSUTVAVBXHVQXBXDVPVQXGVCXEXFUPWBXJUBWHWI
      XJUQVDVEVFVGWTWMBWCGWNWCPZWPWFWSWLXLWOWEWBWNWCVHVIXLWRWKDFXLWQWJWHWNWCWIS
      VJVKVLVMVNVO $.
      $( [16-Apr-202] $) $( [5-Dec-2011] $)
  $}

  ${
    $d A i $.  $d F f i t x y $.  $d I i t x $.
    $( Definition of the product topology of a family of topologies ` F ` .
       (Contributed by FL, 4-Dec-2011.) $)
    istopx $p |- ( ( F : I --> Top /\ I e. A )
      -> ( topX ` F ) = |^| { t e. Top | (
        U. t = X_ x e. I U. ( F ` x ) /\ A. i e. I
          ( X_ x e. I U. ( F ` x ) pr i ) e. ( t Cn ( F ` i ) ) ) } ) $=
      ( vf vy ctop wf wcel ctopx cfv cv cuni cixp wceq cpro co ccn wral wa
      crab cint cdm wi fdm cvv fex c0 wne wrex cpw elisset adantl fvex uniex
      a1i rgen ixpexg sylancl distopg syl unipw w3a ffvelrn adantlr simplr
      eqid weq fveq2 unieqd prmapcp2 sylancom 3jca ex mapdiscn syl6 r19.21aiv
      unieq eqeq1d opreq1 eleq2d ralbidv anbi12d rcla4ev syl12anc rabn0 sylibr
      intex sylib simpl df-prtop feq1 dmeq feq2d bitrd wb fveq1 adantr
      r19.21aiva ixpeq2 ixpeq1 eqtrd eqeq2d opreq1d opreq2d eleq12d raleqbidv
      rabbidva inteqd fvopab6 syl111anc feq2 eleq1 eleq1d raleqbi1dv rabbidv
      imbi12d mpbii anabsi5 ) FIEJZFCKZELMZBNZOZAFANZEMZOZPZQZYJDNZRSZYEYLEMZTS
      ZKZDFUAZUBZBIUCZUDZQZYBEUEZFQZYBYCUBZUUAUFZFIEUGUUCUUBIEJZUUBCKZUBZYDYFAU
      UBYIPZQZUUIYLRSZYOKZDUUBUAZUBZBIUCZUDZQZUFUUEUUHEUHKUUPUHKZUUFUUQUUBICEUI
      UUHUUOUJUKZUURUUHUUNBIULZUUSUUHUUIUMZIKZUVAOZUUIQZUUKUVAYNTSZKZDUUBUAZUUT
      UUHUUIUHKZUVBUUHUUBUHKZYIUHKZAUUBUAZUVHUUGUVIUUFUUBCUNUOUVJAUUBUVJYGUUBKY
      HYGEUPUQURUSZAUUBYIUHUHUTVAUUIUHVBVCUVDUUHUUIVDURUUHUVFDUUBUUHYLUUBKZYNIK
      ZUUIYNOZUUKJZUVHVEZUVFUUHUVMUVQUUHUVMUBZUVNUVPUVHUUFUVMUVNUUGUUBIYLEVFVGU
      UHUVMUVHUVPUVRUUGUVKUVHUUFUUGUVMVHUVLAUUBYICUHUTVAZAUUBYIUVOUUIUHYLUUIVIA
      DVJYHYNYGYLEVKVLVMVNUVSVOVPUUIUVOUHUUKYNUVOVIVQVRVSUUNUVDUVGUBBUVAIYEUVAQ
      ZUUJUVDUUMUVGUVTYFUVCUUIYEUVAVTWAUVTUULUVFDUUBUVTYOUVEUUKYEUVAYNTWBWCWDWE
      WFWGUUNBIWHWIUUOWJWKUUFUUGWLGNZUEZIUWAJZUUFGHEYFAUWBYGUWAMZOZPZQZUWFYLRSZ
      YEYLUWAMZTSZKZDUWBUAZUBZBIUCZUDUUPUHUHLAHBGDWMUWAEQZUWCUWBIEJUUFUWBIUWAEW
      NUWOUWBUUBIEUWAEWOZWPWQUWOUWNUUOUWOUWMUUNBIUWOUWMUUNWRYEIKUWOUWGUUJUWLUUM
      UWOUWFUUIYFUWOUWFAUWBYIPZUUIUWOUWEYIQZAUWBUAUWFUWQQUWOUWRAUWBUWOUWRYGUWBK
      UWOUWDYHYGUWAEWSVLWTXAAUWBUWEYIXBVCUWOUWBUUBQUWQUUIQUWPAUWBUUBYIXCVCXDZXE
      UWOUWKUULDUWBUUBUWPUWOUWHUUKUWJYOUWOUWFUUIYLRUWSXFUWOUWIYNYETYLUWAEWSXGXH
      XIWEWTXJXKXLXMUUCUUHUUDUUQUUAUUCUUFYBUUGYCUUBFIEXNUUBFCXOWEUUCUUPYTYDUUCU
      UOYSUUCUUNYRBIUUCUUJYKUUMYQUUCUUIYJYFAUUBFYIXCZXEUULYPDUUBFUUCUUKYMYOUUCU
      UIYJYLRUWTXFXPXQWEXRXKXEXSXTVCYA $.
      $( [16-Apr-202] $) $( [4-Dec-2011] $)
  $}

  ${
    $d x t i F $.  $d x t i I $.  $d x t i J $.  $d x X a $.  $d A t i $.
    $d i I a $.
    istopxc.1 $e |- X = U. J $.
    $( Product of ` I ` topology ` J ` .  (Contributed by FL, 15-Sep-2013.) $)
    istopxc $p |- ( ( F : I --> { J } /\ I e. A /\ J e. Top )
      -> ( topX ` F ) = |^| { t e. Top | (
        U. t = ( X ^m I ) /\ A. i e. I
          ( ( X ^m I ) pr i ) e. ( t Cn J ) ) } ) $=
      ( vx va csn wf wcel ctop w3a ctopx cfv cv cuni cixp wceq cpro co ccn
      wral wa crab cint cmap wss snssi fss sylan2 3adant2 simp2 istopx
      syl11anc fvconst unieqd syl6eqr ex r19.21aiv 3ad2ant1 ixpeq2 syl cvv
      uniexg syl5eqel ixpeq1 opreq2 eqeq12d ax-1 opreq1 visset ixpconst
      vtocl2g 3adant1 eqtrd eqeq2d adantr opreq1d 3ad2antl1 opreq2d eleq12d
      ralbidva anbi12d rabbidv inteqd ) EFKZDLZEBMZFNMZOZDPQZARZSZIEIRZDQZSZTZU
      AZWTCRZUBUCZWOXBDQZUDUCZMZCEUEZUFZANUGZUHZWPGEUIUCZUAZXKXBUBUCZWOFUDUCZMZ
      CEUEZUFZANUGZUHWMENDLZWKWNXJUAWJWLXSWKWLWJWINUJXSFNUKEWINDULUMUNWJWKWLUOI
      ABCDEUPUQWMXIXRWMXHXQANWMXAXLXGXPWMWTXKWPWMWTIEGTZXKWMWSGUAZIEUEZWTXTUAZW
      JWKYBWLWJYAIEWJWQEMZYAWJYDUFZWSFSZGYEWRFEFWQDURUSHUTVAVBVCZIEWSGVDZVEWKWL
      XTXKUAZWJWLWKGVFMYIWLGYFVFHFNVGVHIXBJRZTZYJXBUIUCZUAIEYJTZYJEUIUCZUAYICJE
      GBVFXBEUAYKYMYLYNIXBEYJVIXBEYJUIVJVKYJGUAZYMXTYNXKYOYOIEUEYMXTUAYOYOIEYOY
      DVLVBIEYJGVDVEYJGEUIVMVKIXBYJCVNJVNVOVPUMVQZVRVSWMXFXOCEWMXBEMZUFZXCXMXEX
      NYRWTXKXBUBYRWTXTXKYRYBYCWMYBYQYGVTYHVEWMYIYQYPVTVRWAYRXDFWOUDWJWKYQXDFUA
      WLEFXBDURWBWCWDWEWFWGWHVR $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d A i t $.  $d F i t x $.  $d I i t x $.
    $( The product topology of a family ` F ` of topologies is a topology.
       (Contributed by FL, 5-Dec-2011.) $)
    prtoptop $p |- ( ( F : I --> Top /\ I e. A )
      -> ( topX ` F ) e. Top ) $=
      ( vt vx vi ctop wf wcel wa ctopx cfv cv cuni cixp wceq cpro co ccn wral
      crab cint istopx c0 wne wss wrex wi csn cpr cun 0ex p0ex unipr uncom un0
      ixp0x eqtr4i 3eqtri ral0 pm3.2i indistop unieq ixpeq1 eqeqan12rd wb
      eleq2 adantr opreq1d opreq1 adantl eleq12d imbi12d ralbidv2 anbi12d
      rcla4edv ex com13 mp2 a1d wn cvv cpw simprr fvex uniex a1i rgen ixpexg
      sylancl distopg ffvelrn ad2antrl imp simplrr eqid weq fveq2 unieqd
      prmapcp2 sylancom mapdiscn syl111anc r19.21aiva unipw jctil eqeq1d
      eleq2d ralbidv mpid com12 3syl pm2.43i pm2.61i rabn0 sylibr ssrab2
      inttop3 eqeltrd ) CGBHZCAIZJZBKLDMZNZECEMZBLZNZOZPZYHFMZQRZYCYJBLZSRZIZFC
      TZJZDGUAZUBZGEDAFBCUCYBYQUDUEZYQGUFYRGIYBYPDGUGZYSCUDPZYBYTUHUUAYTYBUDUDU
      IZUJZNZEUDYGOZPZUUEYJQRZUUCYLSRZIZFUDTZJZUUCGIZUUAYTUHUUFUUJUUDUDUUBUKUUB
      UDUKZUUEUDUUBULUMUNUDUUBUOUUMUUBUUEUUBUPEYGUQURUSUUIFUTVAUUBVBUUAUULUUKYT
      UUAUULUUKYTUHUUAYPUUKDUUCGUUAYCUUCPZJZYIUUFYOUUJUUNUUAYDUUDYHUUEYCUUCVCEC
      UDYGVDZVEUUOYNUUIFCUDUUOYJCIZYJUDIZYNUUIUUAUUQUURVFUUNCUDYJVGVHUUOYKUUGYM
      UUHUUAYKUUGPUUNUUAYHUUEYJQUUPVIVHUUNYMUUHPUUAYCUUCYLSVJVKVLVMVNVOVPVQVRVS
      VTUUAWAZYBYTUUSYBJZYTUUTYHWBIZYHWCZGIZUUTYTUHUUTYAYGWBIZECTZUVAUUSXTYAWDU
      VDECUVDYECIYFYEBWEWFWGWHZECYGAWBWIZWJYHWBWKUUTUVCYTUUTUVCUVBNZYHPZYKUVBYL
      SRZIZFCTZJZYTUUTUVLUVIUUTUVKFCUUTUUQJZYLGIZYHYLNZYKHZUVAUVKUUTUUQUVOXTUUQ
      UVOUHUUSYAXTUUQUVOCGYJBWLVQWMWNUUTUUQUVAUVQUVNYAUVEUVAUUSXTYAUUQWOUVFUVGW
      JZECYGUVPYHWBYJYHWPEFWQYFYLYEYJBWRWSWTXAUVRYHUVPWBYKYLUVPWPXBXCXDYHXEXFUU
      TUVCUVMYTUHUUTYPUVMDUVBGUUTYCUVBPZJZYIUVIYOUVLUVTYDUVHYHUVSYDUVHPUUTYCUVB
      VCVKXGUVSYOUVLVFUUTUVSYNUVKFCUVSYMUVJYKYCUVBYLSVJXHXIVKVOVPVQXJXKXLXMVQXN
      YPDGXOXPYPDGXQYQXRWJXS $.
      $( [5-Dec-2011] $)
  $}

  ${
    $d A i $.  $d F a i t x $.  $d I a i t x $.
    $( The underlying set of a product topology.  (Contributed by FL,
       5-Dec-2011.) $)
    usptop $p |- ( ( F : I --> Top /\ I e. A )
      -> U. ( topX ` F ) = X_ x e. I U. ( F ` x ) ) $=
      ( vt vi va ctop wf wcel wa ctopx cfv cuni cv cixp wceq cpro co ccn wral
      crab cint cpw istopx unieqd wss ssrab2 a1i usptoplem weq unieq eqeq1d
      opreq1 eleq2d ralbidv anbi12d elrab unipw eqcomi eqeq2i biimpi ad2antrl
      sylbi rgen intfmu2 syl111anc 3eqtrd ) DHCIDBJKZCLMZNEOZNZADAOCMNPZQZVMFOZ
      RSZVKVOCMZTSZJZFDUAZKZEHUBZUCZNZVMUDZNZVMVIVJWCAEBFCDUEUFVIWBHUGZWEWBJGOZ
      NZWFQZGWBUAZWDWFQWGVIWAEHUHUIAEBFCDUJWKVIWJGWBWHWBJWHHJZWIVMQZVPWHVQTSZJZ
      FDUAZKZKWJWAWQEWHHEGUKZVNWMVTWPWRVLWIVMVKWHULUMWRVSWOFDWRVRWNVPVKWHVQTUNU
      OUPUQURWMWJWLWPWMWJVMWFWIWFVMVMUSZUTVAVBVCVDVEUIGWBWEVFVGWFVMQVIWSUIVH $.
      $( [5-Dec-2011] $)
  $}

  ${
    $d A i o u $.  $d F i o u x $.  $d F i t u x $.  $d I i o u x $.
    $d I i t u x $.
    $( The projections are continuous.  (Contributed by FL, 18-Apr-2012.) $)
    prcnt $p |- ( ( F : I --> Top /\ I e. A ) -> A. i e. I
      ( X_ x e. I U. ( F ` x ) pr i ) e. ( ( topX ` F ) Cn ( F ` i ) ) ) $=
      ( vo vu vt ctop wf wcel wa cv cfv cuni cixp cpro co ctopx ccn ccnv cima
      wral wb prtoptop adantr ffvelrn adantlr eqid iscn syl11anc cvv simplr
      fvex uniex a1i rgen ixpexg sylancl weq fveq2 unieqd prmapcp2 sylancom
      wceq usptop feq2d mpbird wi wal w3a simprl ad2antrr ra4 ad2antll com12
      ad2antlr imp 3jca jca ex cnima syl6 19.21aiv crab cint istopx eleq2d
      oprex cnvexg imaexg 3syl elintg df-ral syl6bb syl unieq eqeq1d opreq1
      ralbidv anbi12d elrab imbi1i albii 3bitrd r19.21aiv mpbir2and ) EIDJZEBKZ
      LZAEAMZDNZOZPZCMZQRZDSNZXODNZTRKZCEXJXOEKZXSXJXTLZXSXQOZXROZXPJZXPUAZFMZU
      BZXQKZFXRUCZYAXQIKZXRIKZXSYDYILUDXJYJXTBDEUEUFXHXTYKXIEIXODUGUHZFXPXQXRYB
      YCYBUIYCUIUJUKYAYDXNYCXPJZXJXTXNULKZYMYAXIXMULKZAEUCYNXHXIXTUMYOAEYOXKEKX
      LXKDUNUOUPUQAEXMBULURUSAEXMYCXNULXOXNUIACUTXLXRXKXODVAVBVCVDYAYBXNYCXPXJY
      BXNVEXTABDEVFUFVGVHYAYHFXRYAYFXRKZYHYAYPLZYHGMZIKZYROZXNVEZXPYRXRTRZKZCEU
      CZLZLZYGYRKZVIZGVJZYQUUHGYQUUFYSYKUUCVKZYPLZUUGYQUUFUUKYQUUFLZUUJYPUULYSY
      KUUCYQYSUUEVLYAYKYPUUFYLVMYQUUFUUCXTUUFUUCVIXJYPUUFXTUUCUUDXTUUCVIYSUUAUU
      CCEVNVOVPVQVRVSYAYPUUFUMVTWAYFXPYRXRWBWCWDYQYHYGHMZOZXNVEZXPUUMXRTRZKZCEU
      CZLZHIWEZWFZKZYRUUTKZUUGVIZGVJZUUIXJYHUVBUDXTYPXJXQUVAYGAHBCDEWGWHVMYQYGU
      LKZUVBUVEUDYQXPULKZYEULKUVFUVGYQXNXOQWIUPXPULWJYEYFULWKWLUVFUVBUUGGUUTUCU
      VEGYGUUTULWMUUGGUUTWNWOWPUVEUUIUDYQUVDUUHGUVCUUFUUGUUSUUEHYRIHGUTZUUOUUAU
      URUUDUVHUUNYTXNUUMYRWQWRUVHUUQUUCCEUVHUUPUUBXPUUMYRXRTWSWHWTXAXBXCXDUPXEV
      HWAXFXGWAXF $.
      $( [18-Apr-2012] $)
  $}

