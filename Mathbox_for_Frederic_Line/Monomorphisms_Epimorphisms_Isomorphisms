$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Monomorphisms, Epimorphisms, Isomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Epi $. $( The class of all epimorphisms. $)
  $c Monic $. $( The class of all monomorphisms. $)
  $c Iso $. $( The class of all isomorphisms. $)

  $( Extend class notation with the class of all epimorphisms. $)
  cepi $a class Epi $.

  $( Extend class notation with the class of all monomorphisms. $)
  cmon $a class Monic $.

  $( Extend class notation with the class of all isomorphisms. $)
  ciso $a class Iso $.

  ${
    $d x y f g h $.
    $( Function returning the monomorphisms of the category ` x ` .  JFM CAT_1
       def. 10. $)
    df-mon $a |- Monic = { <. x , y >. |
    ( x e. Cat /\ y = { f | ( f e. dom ( dom_ ` x ) /\
      A. g e. dom ( dom_ ` x ) A. h e. dom ( dom_ ` x )
        ( ( ( ( dom_ ` x ) ` g ) = ( ( dom_ ` x ) ` h ) /\
            ( ( cod_ ` x ) ` g ) = ( ( dom_ ` x ) ` f ) /\
            ( ( cod_ ` x ) ` h ) = ( ( dom_ ` x ) ` f ) ) ->
              ( ( f ( o_ ` x ) g ) = ( f ( o_ ` x ) h ) -> g = h ) ) ) } ) } $.
  $}

  ${
    $d x y f g h $.
    $( Function returning the epimorphisms of the category ` x ` .  JFM CAT_1
       def. 11. $)
    df-epi $a |- Epi = { <. x , y >. |
    ( x e. Cat /\ y = { f | ( f e. dom ( dom_ ` x ) /\
      A. g e. dom ( dom_ ` x ) A. h e. dom ( dom_ ` x )
        ( ( ( ( cod_ ` x ) ` g ) = ( ( cod_ ` x ) ` h ) /\
            ( ( dom_ ` x ) ` g ) = ( ( cod_ ` x ) ` f ) /\
            ( ( dom_ ` x ) ` h ) = ( ( cod_ ` x ) ` f ) ) ->
              ( ( g ( o_ ` x ) f ) = ( h ( o_ ` x ) f ) -> g = h ) ) ) } ) } $.
  $}

  ${
    $d x y f g $.
    $( Function returning the isomorphisms of the category ` x ` . $)
    df-isoc $a |- Iso = { <. x , y >. | ( x e. Cat /\
    y = { f e. dom ( dom_ ` x ) | E. g e. dom ( dom_ ` x ) (
     ( dom_ ` g ) = ( cod_ ` f ) /\ ( cod_ ` g ) = ( dom_ ` f ) /\
     ( ( f ( o_ ` x ) g ) = ( ( id_ ` x ) ` ( ( dom_ ` x ) ` g ) ) /\
     ( g ( o_ ` x ) f ) = ( ( id_ ` x ) ` ( ( dom_ ` x ) ` f ) ) ) ) } ) } $.
  $}


  ${
    $d C x y $.  $d D x y $.  $d M f g h x y $.  $d R x y $.  $d T f g h x y $.
    ismona.1 $e |- M = dom ( dom_ ` T ) $.
    ismona.2 $e |- D = ( dom_ ` T ) $.
    ismona.3 $e |- C = ( cod_ ` T ) $.
    ismona.4 $e |- R = ( o_ ` T ) $.
    $( Monomorphisms of a category. $)
    ismona $p |- ( T e. Cat -> ( Monic ` T ) = { f | ( f e. M /\
      A. g e. M A. h e. M
        ( ( ( D ` g ) = ( D ` h ) /\
            ( C ` g ) = ( D ` f ) /\
            ( C ` h ) = ( D ` f ) ) ->
              ( ( f R g ) = ( f R h ) -> g = h ) ) ) } ) $=
      ( vx vy cv cdom_ cfv cdm wcel wceq ccod_ w3a co_ co wi wral wa cab ccat
      cmon fveq2 dmeqd syl6eqr eleq2d raleq1d ralbidv fveq1d eqcomi fveq1i
      syl6eq eqeq12d 3anbi123d opreqd opreq eqcoms ax-mp eqeq12i syl6bb imbi1d
      imbi12d 2ralbidv 3bitrd anbi12d abbidv df-mon cvv fvex dmex eqeltri
      zfausab fvopab4 ) MNDEOZMOZPQZRZSZFOZWDQZGOZWDQZTZWGWCUAQZQZWBWDQZTZWIWLQ
      ZWNTZUBZWBWGWCUCQZUDZWBWIWSUDZTZWGWITZUEZUEZGWEUFZFWEUFZUGZEUHWBHSZWGBQZW
      IBQZTZWGAQZWBBQZTZWIAQZXNTZUBZWBWGCUDZWBWICUDZTZXCUEZUEZGHUFFHUFZUGZEUHUI
      UJWCDTZXHYEEYFWFXIXGYDYFWEHWBYFWEDPQZRZHYFWDYGWCDPUKZULIUMZUNYFXGXEGHUFZF
      WEUFYKFHUFYDYFXFYKFWEYFXEGWEHYJUOUPYFYKFWEHYJUOYFXEYCFGHHYFWRXRXDYBYFWKXL
      WOXOWQXQYFWHXJWJXKYFWHWGYGQXJYFWGWDYGYIUQWGYGBBYGJURZUSUTYFWJWIYGQXKYFWIW
      DYGYIUQWIYGBYLUSUTVAYFWMXMWNXNYFWMWGDUAQZQXMYFWGWLYMWCDUAUKZUQWGYMAAYMKUR
      ZUSUTYFWNWBYGQXNYFWBWDYGYIUQWBYGBYLUSUTZVAYFWPXPWNXNYFWPWIYMQXPYFWIWLYMYN
      UQWIYMAYOUSUTYPVAVBYFXBYAXCYFXBWBWGDUCQZUDZWBWIYQUDZTYAYFWTYRXAYSYFWSYQWB
      WGWCDUCUKZVCYFWSYQWBWIYTVCVAYRXSYSXTCYQTZYRXSTZLUUBYQCWBWGYQCVDVEVFUUAYSX
      TTZLUUCYQCWBWIYQCVDVEVFVGVHVIVJVKVLVMVNMNEFGVOYDEHHYHVPIYGDPVQVRVSVTWA $.
      $( [5-Dec-2007] $) $( [5-Dec-2007] $)
  $}

  ${
    $d C f $.  $d D f $.  $d F f g h $.  $d M f g h $.  $d R f $.
    $d T f g h $.
    ismonb.1 $e |- M = dom ( dom_ ` T ) $.
    ismonb.2 $e |- D = ( dom_ ` T ) $.
    ismonb.3 $e |- C = ( cod_ ` T ) $.
    ismonb.4 $e |- R = ( o_ ` T ) $.
    $( The predicate "is a monomorphism". $)
    ismonb $p |- ( ( T e. Cat /\ F e. A ) -> ( F e. ( Monic ` T )
      <-> ( F e. M /\ A. g e. M A. h e. M
        ( ( ( D ` g ) = ( D ` h ) /\
            ( C ` g ) = ( D ` F ) /\
            ( C ` h ) = ( D ` F ) ) ->
              ( ( F R g ) = ( F R h ) -> g = h ) ) ) ) ) $=
      ( vf ccat wcel wa cmon cfv cv wceq w3a co wi wral cab ismona adantr
      eleq2d wb eleq1 fveq2 eqeq2d 3anbi23d opreq1 eqeq12d imbi1d imbi12d
      2ralbidv anbi12d elabg adantl bitrd ) EOPZHAPZQZHERSZPHNTZIPZFTZCSGTZCSUA
      ZVJBSZVHCSZUAZVKBSZVNUAZUBZVHVJDUCZVHVKDUCZUAZVJVKUAZUDZUDZGIUEFIUEZQZNUF
      ZPZHIPZVLVMHCSZUAZVPWJUAZUBZHVJDUCZHVKDUCZUAZWBUDZUDZGIUEFIUEZQZVFVGWGHVD
      VGWGUAVEBCDENFGIJKLMUGUHUIVEWHWTUJVDWFWTNHAVHHUAZVIWIWEWSVHHIUKXAWDWRFGII
      XAVRWMWCWQXAVOWKVQWLVLXAVNWJVMVHHCULZUMXAVNWJVPXBUMUNXAWAWPWBXAVSWNVTWOVH
      HVJDUOVHHVKDUOUPUQURUSUTVAVBVC $.
      $( [3-Jan-2008] $) $( [2-Jan-2008] $)

    $( The predicate "is a monomorphism". $)
    ismonb1 $p |- ( ( T e. Cat /\ F e. M ) -> ( F e. ( Monic ` T ) <->
      A. g e. M A. h e. M
        ( ( ( D ` g ) = ( D ` h ) /\
            ( C ` g ) = ( D ` F ) /\
            ( C ` h ) = ( D ` F ) ) ->
              ( ( F R g ) = ( F R h ) -> g = h ) ) ) ) $=
      ( ccat wcel wa cmon cfv cv wceq w3a co wi wral ismonb wb ibar adantl
      bitr4d ) DMNZGHNZOGDPQNUJERZBQFRZBQSUKAQGBQZSULAQUMSTGUKCUAGULCUASUKULSUB
      UBFHUCEHUCZOZUNHABCDEFGHIJKLUDUJUNUOUEUIUJUNUFUGUH $.
      $( [3-Jan-2008] $) $( [2-Jan-2008] $)
  $}

  ${
    $d C g j $.  $d D g j $.  $d F g j $.  $d G g j $.  $d J j $.  $d M g j $.
    $d R g j $.  $d T g j $.
    ismonb2.1 $e |- M = dom ( dom_ ` T ) $.
    ismonb2.2 $e |- D = ( dom_ ` T ) $.
    ismonb2.3 $e |- C = ( cod_ ` T ) $.
    ismonb2.4 $e |- R = ( o_ ` T ) $.
    $( A monomorphism is a left-cancelable morphism. $)
    ismonb2 $p |- ( ( T e. Cat /\ ( F e. M /\ G e. M /\ J e. M ) ) ->
      ( F e. ( Monic ` T ) ->
        ( ( ( D ` G ) = ( D ` J ) /\
            ( C ` G ) = ( D ` F ) /\
            ( C ` J ) = ( D ` F ) ) ->
              ( ( F R G ) = ( F R J ) -> G = J ) ) ) ) $=
      ( vg vj ccat wcel w3a wa cmon cfv cv wceq co wi wral wb ismonb1
      3ad2antr1 3simpc adantl fveq2 eqeq1d 3anbi12d opreq2 eqeq1 imbi12d
      eqeq2d 3anbi13d eqeq2 rcla42v syl sylbid ) DOPZEHPZFHPZGHPZQZRZEDSTPZMUAZ
      BTZNUAZBTZUBZVJATZEBTZUBZVLATZVPUBZQZEVJCUCZEVLCUCZUBZVJVLUBZUDZUDZNHUEMH
      UEZFBTZGBTZUBZFATZVPUBZGATZVPUBZQZEFCUCZEGCUCZUBZFGUBZUDZUDZVCVEVDVIWGUFV
      FABCDMNEHIJKLUGUHVHVEVFRZWGXAUDVGXBVCVDVEVFUIUJWFXAWHVMUBZWLVSQZWPWBUBZFV
      LUBZUDZUDMNFGHHVJFUBZVTXDWEXGXHVNXCVQWLVSXHVKWHVMVJFBUKULXHVOWKVPVJFAUKUL
      UMXHWCXEWDXFXHWAWPWBVJFECUNULVJFVLUOUPUPVLGUBZXDWOXGWTXIXCWJVSWNWLXIVMWIW
      HVLGBUKUQXIVRWMVPVLGAUKULURXIXEWRXFWSXIWBWQWPVLGECUNUQVLGFUSUPUPUTVAVB $.
      $( [3-Jan-2008] $) $( [2-Jan-2008] $)
  $}

  ${
    $d B a g h $.  $d C a g h $.  $d F a g h $.  $d H a g h $.  $d O a g h $.
    $d R a $.  $d T a g h $.
    imonclem.1 $e |- O = dom ( id_ ` T ) $.
    imonclem.2 $e |- H = ( hom ` T ) $.
    imonclem.3 $e |- R = ( o_ ` T ) $.
    $( Lemma for ~ ismonc . $)
    imonclem $p |- ( ( T e. Cat
                         /\ ( B e. O /\ C e. O )
                         /\ F e. ( H ` <. B , C >. ) )
                      -> ( A. a
                            e. O
                               A. g
                               e. ( H ` <. a , B >. )
                                  A. h
                                  e. ( H ` <. a , B >. )
                                     ( ( F R g ) = ( F R h ) -> g = h )
                         -> ( F e. dom ( dom_ ` T )
                            /\ A. g
                               e. dom ( dom_ ` T )
                                  A. h
                                  e. dom ( dom_ ` T )
                                     ( ( ( ( dom_ ` T ) ` g )
                                           = ( ( dom_ ` T ) ` h )
                                        /\ ( ( cod_ ` T ) ` g )
                                           = ( ( dom_ ` T ) ` F )
                                        /\ ( ( cod_ ` T ) ` h )
                                           = ( ( dom_ ` T ) ` F ) )
                                     -> ( ( F R g ) = ( F R h ) -> g = h ) ) )
                         ) ) $=
      ( ccat wcel wa cop cfv w3a cv co wceq wi wral cdom_ cdm ccod_ eqid ehm
      3expib 3imp adantr wal ax-17 hbra1 hbral hban 3simp1 dmo expcom mpan9
      r2al ralbii wb opeq1 fveq2d eleq2d anbi12d imbi1d adantl ishomd ad2antrr
      3simp2 pm3.26d syl3anc eqcomd ad2antll eqtr3 ex 3ad2ant3 com12 imp 3jca
      simprl eqidd eqtr 3ad2ant2 simprr jca impbida dehm simplr sylanc 3bitrd
      rcla4dv ax-4 a4s r19.20si syl5com sylbi mpdan com23 19.21ai sylibr ) DNOZ
      AIOZBIOZPZGABQHROZSZGETZCUAGFTZCUAUBXKXLUBUCZFJTZAQZHRZUDZEXPUDZJIUDZGDUE
      RZUFZOZXKXTRZXLXTRZUBZXKDUGRZRZGXTRZUBZXLYFRZYHUBZSZXMUCZFYAUDEYAUDZPXJXS
      PZYBYNXJYBXSXEXHXIYBXEXFXGXIYBUCABDGHYAIKYAUHZLUIUJUKULYOXKYAOZXLYAOZPZYM
      UCZFUMZEUMYNYOUUAEXJXSEXJEUNXREJIXNIOZEUNXQEXPUOUPUQYOYTFXJXSFXJFUNXRFJIU
      UBFUNXQFEXPXKXPOZFUNXMFXPUOUPUPUQXJXSYTXJYSXSYMXJYSXSYMUCZXJYSPZYCIOZUUDX
      JXEYSUUFXEXHXIURZYQXEUUFUCYRXEYQUUFXTDXKYAIYPKXTUHZUSUTULVAZXSUUEUUFPZYMX
      SUUCXLXPOZPZXMUCZFUMZEUMZJIUDZUUJYMUCXRUUOJIXMEFXPXPVBVCUUJUUMJIUDYMUUPUU
      EUUMYMJYCIUUEXNYCUBZPZUUMXKYCAQZHRZOZXLUUTOZPZXMUCZYQYCYCUBZYGAUBZSZYRYDY
      CUBZYJAUBZSZPZXMUCZYMUUQUUMUVDVDUUEUUQUULUVCXMUUQUUCUVAUUKUVBUUQXPUUTXKUU
      QXOUUSHXNYCAVEVFZVGUUQXPUUTXLUVMVGVHVIVJXEUUFXFUVDUVLVDUURXEUUFXFSZUVCUVK
      XMUVNUVAUVGUVBUVJYCAYFXTDXKHYAIKYPUUHYFUHZLVKYCAYFXTDXLHYAIKYPUUHUVOLVKVH
      VIXJXEYSUUQUUGVLUUEUUFUUQUUIULXJXFYSUUQXJXFXGXEXHXIVMVNVLVOUURUVKYLXMYHAU
      BZYSUVKYLVDUURUVPYSPZUVKYLUVQUVKPYEYIYKUVJYEUVQUVGUVJYDYCYRUVHUVIVMVPVQUV
      QUVKYIUVPUVKYIUCYSUVKUVPYIUVGUVPYIUCZUVJUVFYQUVRUVEUVFUVPYIYGYHAVRVSVTULW
      AULWBUVQUVKYKUVPUVKYKUCYSUVKUVPYKUVJUVPYKUCZUVGUVIYRUVSUVHUVIUVPYKYJYHAVR
      VSVTVJWAULWBWCUVQYLPZUVGUVJUVTYQUVEUVFUVQYQYLUVPYQYRWDULUVTYCWEUVQYLUVFUV
      PYLUVFUCYSYLUVPUVFYIYEUVPUVFUCYKYIUVPUVFYGYHAWFVSWGWAULWBWCUVTYRUVHUVIUVQ
      YRYLUVPYQYRWHULYLUVHUVQYLYCYDYEYIYKURVPVJUVQYLUVIUVPYLUVIUCYSYLUVPUVIYKYE
      UVPUVIUCYIYKUVPUVIYJYHAWFVSVTWAULWBWCWIWJXJUVPYSUUQXEXHXIUVPXEXFXGXIUVPUC
      ABXTDGHIKUUHLWKUJUKVLXJYSUUQWLWMVIWNWOUUOUUMJIUUNUUMEUUMFWPWQWRWSWTWAXAVS
      XBWBXCXCYMEFYAYAVBXDWIVS $.
      $( [3-Jan-2008] $) $( [1-Jan-2008] $)
  $}

  ${
    $d B a g h $.  $d C a g h $.  $d F a g h $.  $d H a g h $.  $d O a g h $.
    $d R a $.  $d T a g h $.
    ismonc.1 $e |- O = dom ( id_ ` T ) $.
    ismonc.2 $e |- H = ( hom ` T ) $.
    ismonc.3 $e |- R = ( o_ ` T ) $.
    $( The predicate "is a monomorphism" when the morphism belongs to a
       homset. $)
    ismonc $p |- ( ( T e. Cat /\ ( B e. O /\ C e. O ) /\
      F e. ( H ` <. B , C >. ) ) -> ( F e. ( Monic ` T ) <->
        A. a e. O A. g e. ( H ` <. a , B >. ) A. h e. ( H ` <. a , B >. )
        ( ( F R g ) = ( F R h ) -> g = h ) ) ) $=
      ( ccat wcel wa cop cfv w3a cmon cdom_ cdm cv wceq ccod_ co wi wral wb
      eqid ismonb 3adant2 wal ax-17 hbra1 hban hbral ra42 pm3.31 ishomd biimpd
      anim12d 3simp1 ad2antrr simplr 3simp2 pm3.26d syl3anc anim12i a1i dehm
      eqtr3 syl2an adantl ex 3ad2ant3 adantr impcom 3jca syl6 3expib 3imp jcad
      syld imim1d com13 3syl 19.21ai exp31 com23 imp 19.21aiv r2al ralbii
      df-ral bitri sylibr imonclem impbid bitrd ) DNOZAIOZBIOZPZGABQHRZOZSZGDTR
      OZGDUARZUBZOZEUCZXIRZFUCZXIRZUDZXLDUERZRZGXIRZUDZXNXQRZXSUDZSZGXLCUFGXNCU
      FUDXLXNUDUGZUGZFXJUHZEXJUHZPZYDFJUCZAQHRZUHEYJUHZJIUHZXAXFXHYHUIXDXEXQXIC
      DEFGXJXJUJZXIUJZXQUJZMUKULXGYHYLXGYHYLXGYHPZYIIOZXLYJOZXNYJOZPZYDUGZFUMZE
      UMZUGZJUMZYLYPUUDJXGYHUUDXGYQYHUUCXGYQYHUUCXGYQPZYHPZUUBEUUFYHEUUFEUNXKYG
      EXKEUNYFEXJUOUPUPUUGUUAFUUFYHFUUFFUNXKYGFXKFUNYFFEXJXLXJOZFUNYEFXJUOUQUPU
      PYHUUFUUAYGXKUUFUUAUGZYGUUHXNXJOZPZYEUGUUKYCPZYDUGZXKUUIUGYEEFXJXJURUUKYC
      YDUSUUFXKUUMUUAUUFXKUUMUUAUGUUFXKPZYTUULYDUUNYTUUHXMYIUDZXRAUDZSZUUJXOYIU
      DZYAAUDZSZPZUULXAYQXBYTUVAUGUUNXAYQXBSZYRUUQYSUUTUVBYRUUQYIAXQXIDXLHXJIKY
      MYNYOLUTVAUVBYSUUTYIAXQXIDXNHXJIKYMYNYOLUTVAVBXGXAYQXKXAXDXFVCVDXGYQXKVEX
      GXBYQXKXGXBXCXAXDXFVFVGVDVHUUNUVAUUKYCUVAUUKUGUUNUUQUUHUUTUUJUUHUUOUUPVCU
      UJUURUUSVCVIVJXGUVAYCUGZYQXKXAXDXFUVCXAXBXCXFUVCUGXAXBXCSXFXSAUDZUVCABXID
      GHIKYNLVKUVDUVAYCUVDUVAPXPXTYBUVAXPUVDUUOUURXPUUQUUTXMXOYIVLUUHUUOUUPVFUU
      JUURUUSVFVMVNUVAUVDXTUUQUVDXTUGZUUTUUPUUHUVEUUOUUPUVDXTXRXSAVLVOVPVQVRUVA
      UVDYBUUTUVDYBUGZUUQUUSUUJUVFUURUUSUVDYBYAXSAVLVOVPVNVRVSVOVTWAWBVDWCWDWEV
      OWFWGVRVRWHWHWIWJWKWLYLUUCJIUHUUEYKUUCJIYDEFYJYJWMWNUUCJIWOWPWQVOABCDEFGH
      IJKLMWRWSWT $.
      $( [3-Jan-2008] $) $( [2-Jan-2008] $)
  $}


  ${
    $d A a g h $.  $d B a g h k l $.  $d C a g h k l $.  $d F a g h k l $.
    $d G a g h k l $.  $d H a g h k l $.  $d O a g h k l $.  $d R a g h k l $.
    $d T a g h k l $.
    cmpmon.1 $e |- O = dom ( id_ ` T ) $.
    cmpmon.2 $e |- H = ( hom ` T ) $.
    cmpmon.3 $e |- R = ( o_ ` T ) $.
    $( The composite of two monomorphisms is a monomorphism.  JFM CAT_1 th.
       61 $)
    cmpmon $p |- ( ( T e. Cat /\ ( ( A e. O /\ B e. O /\ C e. O ) /\
         ( F e. ( H ` <. A , B >. ) /\ G e. ( H ` <. B , C >. ) )
           /\ ( F e. ( Monic ` T ) /\ G e. ( Monic ` T ) ) ) ) ->
              ( G R F ) e. ( Monic ` T ) ) $=
      ( vg vh va vk vl ccat wcel w3a cop cfv wa cmon co wi cv wceq wral ax-17
      hbra1 hban hbral cmpassoh 3expib com3r com23 com14 exp43 3impd com13
      com34 3imp adantr imp31 ad2antrr eqcom biimpi adantl imp eqcomd 3exp1
      com3l ex com12 com4r impcom 3impia 3eqtr2d opreq2 eqeq1d eqeq1 imbi12d
      eqeq2d eqeq2 rcla42v homgrf 3simpa syl5 adantrd com24 3adant3 jca ra4
      ad2antll sylc wal r3al 19.21bi 19.21bbi ad2antrl 3expd imp41 syld
      r19.21ai wb ismonc 3simp1 3simpb 3ad2ant2 syl3anc sylibrd biimpd 3exp
      3simpc syl2and 3imp2 ) ERSZAISZBISZCISZTZFABUAHUBSZGBCUAHUBSZUCZFEUDUBZSZ
      GYFSZUCZGFDUEZYFSZXRYBYEYIYKUFXRYBYETZFMUGZDUEZFNUGZDUEZUHZYMYOUHZUFZNOUG
      ZAUAHUBZUIZMUUAUIZOIUIZGPUGZDUEZGQUGZDUEZUHZUUEUUGUHZUFZQYTBUAHUBZUIPUULU
      IZOIUIZYKYGYHYLUUDUUNUCZYJYMDUEZYJYODUEZUHZYRUFZNUUAUIZMUUAUIZOIUIZYKYLUU
      OUVBYLUUOUCZUVAOIYLUUOOYLOUJUUDUUNOUUCOIUKUUMOIUKULULUVCYTISZUVAUVCUVDUCZ
      UUTMUUAUVCUVDMYLUUOMYLMUJUUDUUNMUUCMOIUVDMUJZUUBMUUAUKUMUUNMUJULULUVFULUV
      EYMUUASZUUTUVEUVGUCZUUSNUUAUVEUVGNUVCUVDNYLUUONYLNUJUUDUUNNUUCNOIUVDNUJZU
      UBNMUUAUVGNUJZYSNUUAUKUMUMUUNNUJULULUVIULUVJULUVHYOUUASZUUSUVHUVKUCZUURGY
      NDUEZGYPDUEZUHZYRUVLUURUVOUVLUURUCUVMUUPUUQUVNUVHUVMUUPUHZUVKUURUVCUVDUVG
      UVPYLUVDUVGUVPUFZUFZUUOXRYBYEUVRXRYBUVDYEUVQUVDYBXRYEUVQUFZUVDXSXTYAXRUVS
      UFZUVDXSXTYAUVTUVGXRYEUVDXSUCZXTYAUCZUCZUVPUVGYEXRUWCUVPUFZUVGYCYDXRUWDUF
      XRUWCUVGYCYDTZUVPXRUWAUWBUWEUVPUFYTABCDEHYMFGIJKLUNUOUPUOUQURUSUTVAVBVCVD
      VEVFUURUUQUUPUHZUVLUURUWFUUPUUQVGVHVIUVLUUQUVNUHZUURUVHUVKUWGUVEUVKUWGUFZ
      UVGUVCUVDUWHYLUVDUWHUFZUUOXRYBYEUWIUVKYEUVDXRYBUCZUWGUVKYCYDUVDUWJUWGUFUF
      UWJUVDUVKYCYDTZUWGYBXRUVDUWKUWGUFZUFZXSXTYAXRUWMUFXTYAXRXSUWMXTYAXRXSUWMU
      FZUFXRUWBUWNUVDUWBXSXRUWLUVDXSUWBXRUWLUFZUVDXSUWBUWOUFXRUWAUWBUWLXRUWAUWB
      UWKUWGXRUWAUWBTZUWKUCUVNUUQUWPUWKUVNUUQUHYTABCDEHYOFGIJKLUNVJVKVLVMVNUQUR
      VOVNVPVCVQVAUOURVRVDVJVDVJVDVSVNUVLUVOYQYRYNUULSZYPUULSZUCUUMUVOYQUFZUVLU
      UKUWSUVMUUHUHZYNUUGUHZUFPQYNYPUULUULUUEYNUHZUUIUWTUUJUXAUXBUUFUVMUUHUUEYN
      GDVTWAUUEYNUUGWBWCUUGYPUHZUWTUVOUXAYQUXCUUHUVNUVMUUGYPGDVTWDUUGYPYNWEWCWF
      UVLUWQUWRUVHUWQUVKUVCUVDUVGUWQYLUVDUVGUWQUFZUFZUUOXRYBYEUXEYEYBXRUXEYCYBX
      RUXEUFZUFYDYCXSXTUCZUXFYBUVDUXGXRYCUXDUVDXSXTXRYCUXDUFUFUVGXRYCUVDXSXTTZU
      WQUVGYCXRUXHUWQUFZUVGYCXRUXIUFXRUXHUVGYCUCZUWQXRUXHUXJUWQUFYTABDEYMFHIJKL
      WGVNUPVNUQURUOURXSXTYAWHZWIVDVAVCVDVEVDUVHUVKUWRUVEUVKUWRUFZUVGUVCUVDUXLY
      LUVDUXLUFZUUOXRYBYEUXMYBXRYEUXMUFZXSXTXRUXNUFYAUXGUVDYEXRUXLUVDUXGYEXRUXL
      UFZUFZUVDXSXTUXPUXHYCUXOYDUVKYCXRUXHUWRUVKYCXRUXHUWRUFUFXRUXHUVKYCUCZUWRX
      RUXHUXQUWRUFYTABDEYOFHIJKLWGVNUPVNURWJUOVOWKWLVOVCVDVJVDVJWMUVEUUMUVGUVKU
      VCUVDUUMUUNUVDUUMUFYLUUDUUMOIWNWOVJVFWPUVCUVDUVGUVKYSUVCUVDUVGUVKYSUUDUVD
      UVGUVKTYSUFZYLUUNUUDUXRMNUUDUXRNWQMWQZOUUDUXSOWQYSOMNIUUAUUAWRVHWSWTXAXBX
      CXDXDVNXEVNXEVNXEVNXRXSYAUCZYJACUAHUBSZYKUVBXFYLACDEMNYJHIOJKLXGXRYBYEXHY
      BXRUXTYEXSXTYAXIXJXRYBYEUYAABCDEFGHIJKLWGVRXKXLXRYBYEYGUUDUFZXRUXGYEUYBUF
      YBYEUXGXRUYBYCUXGXRUYBUFUFYDXRUXGYCUYBXRUXGYCUYBXRUXGYCTYGUUDABDEMNFHIOJK
      LXGXMXNVAVDVAUXKWIVCXRYBYEYHUUNUFZXRUWBYEUYCUFYBYEUWBXRUYCYDUWBXRUYCUFUFY
      CXRUWBYDUYCXRUWBYDUYCXRUWBYDTYHUUNBCDEPQGHIOJKLXGXMXNVAVIVAXSXTYAXOWIVCXP
      XNXQ $.
      $( [10-Mar-2008] $) $( [10-Mar-2008] $)
  $}

  ${
    $d A a m n $.  $d B a m n $.  $d C a m n $.  $d F a m n $.  $d G a m n $.
    $d H a m n $.  $d O a m n $.  $d R a m n $.  $d T a m n $.
    icmpmon.1 $e |- O = dom ( id_ ` T ) $.
    icmpmon.2 $e |- H = ( hom ` T ) $.
    icmpmon.3 $e |- R = ( o_ ` T ) $.
    $( If ` ( G R F ) ` is a monomorphism then ` F ` is a monomorphism.  JFM
       CAT_1 th. 62 $)
    icmpmon $p |- ( ( T e. Cat /\ ( ( A e. O /\ B e. O /\ C e. O ) /\
         ( F e. ( H ` <. A , B >. ) /\ G e. ( H ` <. B , C >. ) )
           /\ ( G R F ) e. ( Monic ` T ) ) ) ->
              F e. ( Monic ` T ) ) $=
      ( vm vn va ccat wcel w3a cop cfv wa co cmon cv wceq wi wral wb ismonc
      3simp1 3simpb 3ad2ant2 homgrf 3impia syl3anc pm3.27 simpll simplr
      3eqtr4d ex imim1d opreq2 syl7 cmpassoh 3exp com3l exp4b 3impd com13 imp
      3expib com14 imp31 eqcomd adantr 3exp1 com23 3imp sylanc r19.20dva
      sylbid 3imp2 pm3.26 3simpa 3ad2ant1 adantl 3simp2 pm3.26d mpbird ) EPQZAI
      QZBIQZCIQZRZFABSHTQZGBCSHTQZUAZGFDUBZEUCTZQZRZUAZFWSQZFMUDZDUBZFNUDZDUBZU
      EZXDXFUEZUFZNOUDZASHTZUGZMXLUGZOIUGZWJWNWQWTXOWJWNWQWTXOUFWJWNWQRZWTWRXDD
      UBZWRXFDUBZUEZXIUFZNXLUGZMXLUGZOIUGZXOWJWKWMUAZWRACSHTQZWTYCUHXPACDEMNWRH
      IOJKLUIWJWNWQUJWNWJYDWQWKWLWMUKULWJWNWQYEABCDEFGHIJKLUMUNUOXPYBXNOIXPXKIQ
      ZUAZYAXMMXLYGXDXLQZUAZXTXJNXLXQGXEDUBZUEZXRGXGDUBZUEZXTXJUFYIXFXLQZUAYKYM
      UAZXTYJYLUEZXIXHYOYPXSXIYOYPXSYOYPUAYJYLXQXRYOYPUPYKYMYPUQYKYMYPURUSUTVAX
      EXGGDVBVCYIYKYNYIYJXQXPYFYHYJXQUEZWJWNWQYFYHYQUFUFYHWQYFWJWNUAZYQYHWOWPYF
      YRYQUFUFYRYFYHWOWPRZYQWJWNYFYSYQUFZUFYFWNWJYTYFWKWLWMWJYTUFZYFWKWLWMUUAWJ
      YFWKUAZWLWMUAZYTWJUUBUUCYTXKABCDEHXDFGIJKLVDVEVFVGVHVIVJVIVKVLUNVMVNVOYIY
      NYMYGYNYMUFZYHXPYFUUDXPYNYFYMWJWNWQYNYFYMUFZUFYNWNWQWJUUEYNWQWNWJUUEUFZYN
      WOWPWNUUFUFYFWNWJYNWOWPRZYMYFWKWLWMWJUUGYMUFZUFZYFWKWLWMUUIWJUUBUUCUUHWJU
      UBUUCUUGYMWJUUBUUCRZUUGUAYLXRUUJUUGYLXRUEXKABCDEHXFFGIJKLVDVJVNVPVFVGVHVL
      VKVQVLVRVQVJVOVJVSVTVTVTWAVEWBWJWKWLUAZWOXCXOUHXBABDEMNFHIOJKLUIWJXAWCXAU
      UKWJWNWQUUKWTWKWLWMWDWEWFXAWOWJXAWOWPWNWQWTWGWHWFUOWI $.
      $( [27-Apr-2008] $) $( [17-Mar-2008] $)
  $}

  ${
    $d A c f g $.  $d B c f g $.  $d F c f g $.  $d G c f g $.  $d H c f g $.
    $d J c f g $.  $d O c f g $.  $d R c f g $.  $d T c f g $.
    idmon.1 $e |- O = dom ( id_ ` T ) $.
    idmon.2 $e |- H = ( hom ` T ) $.
    idmon.3 $e |- R = ( o_ ` T ) $.
    idmon.4 $e |- J = ( id_ ` T ) $.
    $( If there exists ` G ` such as ` ( G R F ) = ( J `` B ) ` then F is a
       monomorphism.  JFM CAT_1 th. 63. $)
    idmon $p |- ( ( T e. Cat /\ ( A e. O /\ B e. O ) /\
      ( G e. ( H ` <. A , B >. ) /\ F e. ( H ` <. B , A >. ) ) ) ->
      ( ( G R F ) = ( J ` B ) -> F e. ( Monic ` T ) ) ) $=
      ( vf vg vc ccat wcel wa cop cfv w3a co wceq cmon cv wi wral cmpassoh
      3simp1 adantr ad2antrr simprl simplr 3ad2antl2 jca simpll 3jca simprr
      3ad2antl3 sylc pm3.27 eqeq12d opreq1d biimpd cmphmia syl3anc ex imp32
      cdm cdom_ ccod_ eqid cmpida cid_ eqcomi dmeqi eqtri eleq2i biimpi adantl
      3ad2ant2 ehm imp cehm sylibd sylbid opreq2 syl5 r19.21aiva r19.21aivv wb
      ismonc mpbird ) DQRZAIRZBIRZSZFABTGUARZEBATGUARZSZUBZFECUCZBHUAZUDZEDUEUA
      RZXBXESZXFENUFZCUCZEOUFZCUCZUDZXHXJUDZUGZOPUFZBTGUAZUHZNXPUHPIUHZXGXQPNIX
      PXGXOIRZXHXPRZSZXQXGYASZXNOXPYBXJXPRZSZFXICUCZFXKCUCZUDZXMXLYDYGXCXHCUCZX
      CXJCUCZUDZXMYDYEYHYFYIWOXSWQSZWRUBZXTWTWSUBYEYHUDYDXOBABCDGXHEFIJKLUIYDWO
      YKWRXGWOYAYCXBWOXEWOWRXAUJZUKZULZYDXSWQYBXSYCXGXSXTUMZUKXGWQYAYCWRWOXEWQX
      AWPWQXEUNUOZULZUPYDWPWQXGWPYAYCWRWOXEWPXAWPWQXEUQUOZULYRUPURZYDXTWTWSYBXT
      YCXGXSXTUSUKXGWTYAYCXAWOXEWTWRWSWTXEUNUTZULZXGWSYAYCXAWOXEWSWRWSWTXEUQUTU
      LZURVAYLYCWTWSUBYFYIUDYDXOBABCDGXJEFIJKLUIYTYDYCWTWSYBYCVBUUBUUCURVAVCYDY
      JXDXHCUCZXDXJCUCZUDZXMYDYJUUFYDYHUUDYIUUEYDXCXDXHCYBXEYCXBXEYAUNUKZVDYDXC
      XDXJCUUGVDVCVEYDUUDXHUUEXJYBUUDXHUDZYCXGXSXTUUHXGXSXTUUHUGZWOXSWQUUIXGXSS
      XOBCDXHGHIJKMLVFXBWOXEXSYMULXGXSVBXGWQXSYQUKVGVHVIUKWOBHVJZRZXJDVKUAZVJZR
      ZUBXJDVLUAZUABUDZUUEXJUDYDBUUOUULCDXJHUUMUUJUUMVMZUULVMUUJVMMLUUOVMZVNYDW
      OUUKUUNYOXGUUKYAYCXBUUKXEWRWOUUKXAWQUUKWPWQUUKIUUJBIDVOUAZVJUUJJUUSHHUUSM
      VPVQVRVSVTWAWBUKULYBYCUUNWOXSWQYCUUNUGYBXOBDXJGUUMIJUUQKWCXBWOXEYAYMULZYP
      XGWQYAYQUKZVGWDURYBYCUUPWOXSWQYCUUPUGYBXOBUUODXJGIJUURKWEUUTYPUVAVGWDVAVC
      WFWGXIXKFCWHWIWJVHWKWOWQWPSWTXFXRWLXGBACDNOEGIPJKLWMYNXGWQWPYQYSUPUUAVGWN
      VH $.
      $( [1-Jun-2008] $) $( [5-May-2008] $)
  $}

  ${
    immon.1 $e |- O = dom ( id_ ` T ) $.
    immon.2 $e |- J = ( id_ ` T ) $.
    $( A morphism identity is a monomorphism.  JFM CAT_1 th. 64. $)
    immon $p |- ( ( T e. Cat /\ A e. O ) -> ( J ` A ) e. ( Monic ` T ) ) $=
      ( ccat wcel wa cfv cop chom w3a co_ co wceq cmon eqid idmon pm3.26
      pm3.27 jca homib 3jca iri sylc ) BGHZADHZUHIZACJZAAKBLJZJHZULIZMUJUJBNJZO
      UJPUJBQJHUGUHIZAAUNBUJUJUKCDEUKRZUNRZFSUOUGUIUMUGUHTUOUHUHUGUHUAZURUBUOUL
      ULABUKCDEFUPUCZUSUBUDAUNBCDEFUQUEUF $.
      $( [1-Jun-2008] $) $( [5-May-2008] $)
  $}

  ${
    $d C x y $.  $d D x y $.  $d M f g h x y $.  $d R x y $.  $d T f g h x y $.
    isepia.1 $e |- M = dom ( dom_ ` T ) $.
    isepia.2 $e |- D = ( dom_ ` T ) $.
    isepia.3 $e |- C = ( cod_ ` T ) $.
    isepia.4 $e |- R = ( o_ ` T ) $.
    $( Epimorphisms of a category ` T ` . $)
    isepia $p |- ( T e. Cat -> ( Epi ` T ) = { f | ( f e. M /\
      A. g e. M A. h e. M
        ( ( ( C ` g ) = ( C ` h ) /\
            ( D ` g ) = ( C ` f ) /\
            ( D ` h ) = ( C ` f ) ) ->
              ( ( g R f ) = ( h R f ) -> g = h ) ) ) } ) $=
      ( vx vy cv cdom_ cfv cdm wcel ccod_ wceq w3a co_ co wi wral wa cab ccat
      cepi fveq2 dmeqd syl6eqr eleq2d fveq1d eqcomi a1i eqtrd eqeq12d
      3anbi123d opreqd imbi1d imbi12d 2ralbidv raleq1d ralbidv 3bitrd anbi12d
      abbidv df-epi cvv fvex dmex eqeltri zfausab fvopab4 ) MNDEOZMOZPQZRZSZFOZ
      VRTQZQZGOZWCQZUAZWBVSQZVQWCQZUAZWEVSQZWIUAZUBZWBVQVRUCQZUDZWEVQWNUDZUAZWB
      WEUAZUEZUEZGVTUFFVTUFZUGZEUHVQHSZWBAQZWEAQZUAZWBBQZVQAQZUAZWEBQZXHUAZUBZW
      BVQCUDZWEVQCUDZUAZWRUEZUEZGHUFZFHUFZUGZEUHUIUJVRDUAZXBXTEYAWAXCXAXSYAVTHV
      QYAVTDPQZRZHYAVSYBVRDPUKZULIUMZUNYAXAXQGVTUFZFVTUFXRFVTUFXSYAWTXQFGVTVTYA
      WMXLWSXPYAWGXFWJXIWLXKYAWDXDWFXEYAWDWBDTQZQXDYAWBWCYGVRDTUKZUOYAWBYGAYGAU
      AYAAYGKUPUQZUOURYAWEWCAYAWCYGAYHKUMUOUSYAWHXGWIXHYAWBVSBYAVSYBBYDJUMZUOYA
      WIVQYGQXHYAVQWCYGYHUOYAVQYGAYIUOURZUSYAWKXJWIXHYAWEVSBYJUOYKUSUTYAWQXOWRY
      AWOXMWPXNYAWOWBVQDUCQZUDXMYAWNYLWBVQVRDUCUKZVAYAYLCWBVQYLCUAYACYLLUPUQZVA
      URYAWPWEVQYLUDXNYAWNYLWEVQYMVAYAYLCWEVQYNVAURUSVBVCVDYAYFXRFVTYAXQGVTHYEV
      EVFYAXRFVTHYEVEVGVHVIMNEFGVJXSEHHYCVKIYBDPVLVMVNVOVP $.
      $( [1-Sep-2008] $) $( [8-Aug-2008] $)
  $}

  ${
    $d C f $.  $d D f $.  $d F f g h $.  $d M f g h $.  $d R f $.
    $d T f g h $.
    isepib.1 $e |- M = dom ( dom_ ` T ) $.
    isepib.2 $e |- D = ( dom_ ` T ) $.
    isepib.3 $e |- C = ( cod_ ` T ) $.
    isepib.4 $e |- R = ( o_ ` T ) $.
    $( The predicate "is an epimorphism". $)
    isepib $p |- ( ( T e. Cat /\ F e. A ) -> ( F e. ( Epi ` T )
      <-> ( F e. M /\ A. g e. M A. h e. M
        ( ( ( C ` g ) = ( C ` h ) /\
            ( D ` g ) = ( C ` F ) /\
            ( D ` h ) = ( C ` F ) ) ->
              ( ( g R F ) = ( h R F ) -> g = h ) ) ) ) ) $=
      ( vf ccat wcel wa cepi cfv cv wceq w3a co wi wral cab isepia adantr
      eleq2d wb eleq1 fveq2 eqeq2d 3anbi23d opreq2 eqeq12d imbi1d imbi12d
      2ralbidv anbi12d elabg adantl bitrd ) EOPZHAPZQZHERSZPHNTZIPZFTZBSGTZBSUA
      ZVJCSZVHBSZUAZVKCSZVNUAZUBZVJVHDUCZVKVHDUCZUAZVJVKUAZUDZUDZGIUEFIUEZQZNUF
      ZPZHIPZVLVMHBSZUAZVPWJUAZUBZVJHDUCZVKHDUCZUAZWBUDZUDZGIUEFIUEZQZVFVGWGHVD
      VGWGUAVEBCDENFGIJKLMUGUHUIVEWHWTUJVDWFWTNHAVHHUAZVIWIWEWSVHHIUKXAWDWRFGII
      XAVRWMWCWQXAVOWKVQWLVLXAVNWJVMVHHBULZUMXAVNWJVPXBUMUNXAWAWPWBXAVSWNVTWOVH
      HVJDUOVHHVKDUOUPUQURUSUTVAVBVC $.
      $( [1-Sep-2008] $) $( [8-Aug-2008] $)
  $}

  ${
    $d F g h $.  $d M g h $.  $d T g h $.
    isepib1.1 $e |- M = dom ( dom_ ` T ) $.
    isepib1.2 $e |- D = ( dom_ ` T ) $.
    isepib1.3 $e |- C = ( cod_ ` T ) $.
    isepib1.4 $e |- R = ( o_ ` T ) $.
    $( The predicate "is an epimorphism". $)
    isepib1 $p |- ( ( T e. Cat /\ F e. M ) -> ( F e. ( Epi ` T ) <->
      A. g e. M A. h e. M
        ( ( ( C ` g ) = ( C ` h ) /\
            ( D ` g ) = ( C ` F ) /\
            ( D ` h ) = ( C ` F ) ) ->
              ( ( g R F ) = ( h R F ) -> g = h ) ) ) ) $=
      ( ccat wcel wa cepi cfv cv wceq w3a co wi wral isepib wb ibar adantl
      bitr4d ) DMNZGHNZOGDPQNUJERZAQFRZAQSUKBQGAQZSULBQUMSTUKGCUAULGCUASUKULSUB
      UBFHUCEHUCZOZUNHABCDEFGHIJKLUDUJUNUOUEUIUJUNUFUGUH $.
      $( [1-Sep-2008] $) $( [10-Aug-2008] $)
  $}

  ${
    $d C g j $.  $d D g j $.  $d F g j $.  $d G g j $.  $d J j $.  $d M g j $.
    $d R g j $.  $d T g j $.
    isepib2.1 $e |- M = dom ( dom_ ` T ) $.
    isepib2.2 $e |- D = ( dom_ ` T ) $.
    isepib2.3 $e |- C = ( cod_ ` T ) $.
    isepib2.4 $e |- R = ( o_ ` T ) $.
    $( An epimorphism is a right-cancelable morphism. $)
    isepib2 $p |- ( ( T e. Cat /\ ( F e. M /\ G e. M /\ J e. M ) ) ->
      ( F e. ( Epi ` T ) ->
        ( ( ( C ` G ) = ( C ` J ) /\
            ( D ` G ) = ( C ` F ) /\
            ( D ` J ) = ( C ` F ) ) ->
              ( ( G R F ) = ( J R F ) -> G = J ) ) ) ) $=
      ( vg vj ccat wcel w3a wa cepi cfv cv wceq co wi wral wb isepib1
      3ad2antr1 3simpc adantl fveq2 eqeq1d 3anbi12d opreq1 eqeq1 imbi12d
      eqeq2d 3anbi13d eqeq2 rcla42v syl sylbid ) DOPZEHPZFHPZGHPZQZRZEDSTPZMUAZ
      ATZNUAZATZUBZVJBTZEATZUBZVLBTZVPUBZQZVJECUCZVLECUCZUBZVJVLUBZUDZUDZNHUEMH
      UEZFATZGATZUBZFBTZVPUBZGBTZVPUBZQZFECUCZGECUCZUBZFGUBZUDZUDZVCVEVDVIWGUFV
      FABCDMNEHIJKLUGUHVHVEVFRZWGXAUDVGXBVCVDVEVFUIUJWFXAWHVMUBZWLVSQZWPWBUBZFV
      LUBZUDZUDMNFGHHVJFUBZVTXDWEXGXHVNXCVQWLVSXHVKWHVMVJFAUKULXHVOWKVPVJFBUKUL
      UMXHWCXEWDXFXHWAWPWBVJFECUNULVJFVLUOUPUPVLGUBZXDWOXGWTXIXCWJVSWNWLXIVMWIW
      HVLGAUKUQXIVRWMVPVLGBUKULURXIXEWRXFWSXIWBWQWPVLGECUNUQVLGFUSUPUPUTVAVB $.
      $( [1-Sep-2008] $) $( [10-Aug-2008] $)
  $}

  ${
    $d A c g h $.  $d B c g h $.  $d F c g h $.  $d H c g h $.  $d O c g h $.
    $d R c $.  $d T c g h $.
    iepiclem.1 $e |- O = dom ( id_ ` T ) $.
    iepiclem.2 $e |- H = ( hom ` T ) $.
    iepiclem.3 $e |- R = ( o_ ` T ) $.
    $( Lemma for ~ isepic . $)
    iepiclem $p |- ( ( T e. Cat
                         /\ ( A e. O /\ B e. O )
                         /\ F e. ( H ` <. A , B >. ) )
                      -> ( A. c
                            e. O
                               A. g
                               e. ( H ` <. B , c >. )
                                  A. h
                                  e. ( H ` <. B , c >. )
                                     ( ( g R F ) = ( h R F ) -> g = h )
                         -> ( F e. dom ( dom_ ` T )
                            /\ A. g
                               e. dom ( dom_ ` T )
                                  A. h
                                  e. dom ( dom_ ` T )
                                     ( ( ( ( cod_ ` T ) ` g )
                                           = ( ( cod_ ` T ) ` h )
                                        /\ ( ( dom_ ` T ) ` g )
                                           = ( ( cod_ ` T ) ` F )
                                        /\ ( ( dom_ ` T ) ` h )
                                           = ( ( cod_ ` T ) ` F ) )
                                     -> ( ( g R F ) = ( h R F ) -> g = h ) ) )
                         ) ) $=
      ( ccat wcel wa cop cfv w3a cv co wceq wi wral cdom_ cdm ccod_ eqid ehm
      3expib 3imp adantr wal ax-17 hbra1 hbral hban cdmo ex adantrd 3ad2ant1
      imp opeq2 fveq2d eleq2d anbi12d wb anim12d ancrd ishomd 3exp com12
      adantl impcom 3adant3 adantrr biimpd eqeq2 eqcoms 3anbi2d eqcom biimpi
      3ad2ant3 3simp2 3jca simpll eqidd simplr 3simp3 jca impbid2 sylan9bb syl
      bitrd syld sylan9bbr imbi1d rcla4dv ra42 r19.20si syl5 mpdan com23
      19.21ai r2al sylibr ) DNOZAIOZBIOZPZGABQHROZSZETZGCUAFTZGCUAUBXMXNUBUCZFB
      JTZQZHRZUDZEXRUDZJIUDZGDUERZUFZOZXMDUGRZRZXNYERZUBZXMYBRZGYERZUBZXNYBRZYJ
      UBZSZXOUCZFYCUDEYCUDZPXLYAPZYDYPXLYDYAXGXJXKYDXGXHXIXKYDUCABDGHYCIKYCUHZL
      UIUJUKULYQXMYCOZXNYCOZPZYOUCZFUMZEUMYPYQUUCEXLYAEXLEUNXTEJIXPIOZEUNXSEXRU
      OUPUQYQUUBFXLYAFXLFUNXTFJIUUDFUNXSFEXRXMXROZFUNXOFXRUOUPUPUQXLYAUUBXLUUAY
      AYOXLUUAYAYOUCZXLUUAPZYFIOZUUFXLUUAUUHXGXJUUAUUHUCXKXGYSUUHYTXGYSUUHYEDXM
      YCIYRKYEUHZURUSZUTVAVBUUGUUHPUUEXNXROZPZXOUCZJIUDYOYAUUGUUMYOJYFIUUGXPYFU
      BZPUULYNXOUUNUULXMBYFQZHRZOZXNUUPOZPZUUGYNUUNUUEUUQUUKUURUUNXRUUPXMUUNXQU
      UOHXPYFBVCVDZVEUUNXRUUPXNUUTVEVFXLUUAUUSYNVGZXLUUAUUHYGIOZPZUUAPZUVAXLUUA
      UVCXGXJUUAUVCUCXKXGYSUUHYTUVBUUJXGYTUVBYEDXNYCIYRKUUIURUSVHVAVIXLUVDUVAXL
      UVDPUUSYSYIBUBZYFYFUBZSZYTYLBUBZYGYFUBZSZPZYNXLUVCUUSUVKVGUUAXLUVCPUUQUVG
      UURUVJUVCXLUUQUVGVGZUUHXLUVLUCUVBXLUUHUVLXGXJUUHUVLUCZXKXJXGUVMXIXGUVMUCX
      HXGXIUVMXGXIUUHUVLBYFYEYBDXMHYCIKYRYBUHZUUILVJVKVLVMVNVOVLULVNUVCXLUURUVJ
      VGZUUHXLUVOUCUVBXLUUHUVOXGXJUUHUVOUCZXKXJXGUVPXIXGUVPUCXHXGXIUVPXGXIUUHUV
      OBYFYEYBDXNHYCIKYRUVNUUILVJVKVLVMVNVOVLULVNVFVPUVDXLUVKYNVGZUUAXLUVQUCUVC
      XLUUAUVQXLYDGYBRAUBZYJBUBZSZUUAUVQUCZXGXJXKUVTXGXHXIXKUVTUCXGXHXISXKUVTAB
      YEYBDGHYCIKYRUVNUUILVJVQUJUKUVSYDUWAUVRUVSUUAUVQUVSUVKYSYKUVFSZYTYMUVISZP
      ZUUAYNUVSUVGUWBUVJUWCUVSUVEYKYSUVFUVEYKVGBYJBYJYIVRVSVTUVSUVHYMYTUVIUVHYM
      VGBYJBYJYLVRVSVTVFUUAUWDYNUWDYHYKYMUWCYHUWBUVIYTYHYMUVIYHYGYFWAWBWCVMUWBY
      KUWCYSYKUVFWDULUWCYMUWBYTYMUVIWDVMWEUUAYNUWDUUAYNPZUWBUWCUWEYSYKUVFYSYTYN
      WFYNYKUUAYHYKYMWDVMUWEYFWGWEUWEYTYMUVIYSYTYNWHYNYMUUAYHYKYMWIVMYNUVIUUAYH
      YKUVIYMYHUVIYFYGWAWBVAVMWEWJUSWKWLUSWCWMVLVMVNWNUSWOVBWPWQWRXTUUMJIXOEFXR
      XRWSWTXAXBUSXCVBXDXDYOEFYCYCXEXFWJUS $.
      $( [7-Oct-2008] $) $( [6-Oct-2008] $)
  $}

  ${
    $d A c g h $.  $d B c g h $.  $d F c g h $.  $d H c g h $.  $d O c g h $.
    $d R c $.  $d T c g h $.
    isepic.1 $e |- O = dom ( id_ ` T ) $.
    isepic.2 $e |- H = ( hom ` T ) $.
    isepic.3 $e |- R = ( o_ ` T ) $.
    $( The predicate "is an epimorphism" when the morphism belongs to a
       homset. $)
    isepic $p |- ( ( T e. Cat /\ ( A e. O /\ B e. O ) /\
      F e. ( H ` <. A , B >. ) ) -> ( F e. ( Epi ` T ) <->
        A. c e. O A. g e. ( H ` <. B , c >. ) A. h e. ( H ` <. B , c >. )
        ( ( g R F ) = ( h R F ) -> g = h ) ) ) $=
      ( ccat wcel wa cop cfv w3a cepi cdom_ cdm cv ccod_ wceq co wi wral wb
      eqid isepib 3adant2 wal ax-17 hbra1 hban hbral ra42 pm3.31 ishomd biimpd
      anim12d 3simp1 ad2antrr pm3.27 3ad2ant2 simplr syl3anc anim12i a1i cehm
      eqtr expcom eqcoms 3ad2ant3 com12 imp adantl adantr impcom 3jca ex syl6
      3expib 3imp jcad syld imim1d com13 3syl 19.21ai exp31 com23 19.21aiv
      r2al ralbii df-ral bitri sylibr iepiclem impbid bitrd ) DNOZAIOZBIOZPZGAB
      QHRZOZSZGDTROZGDUARZUBZOZEUCZDUDRZRZFUCZXORZUEZXNXKRZGXORZUEZXQXKRZYAUEZS
      ZXNGCUFXQGCUFUEXNXQUEUGZUGZFXLUHZEXLUHZPZYFFBJUCZQHRZUHEYLUHZJIUHZXCXHXJY
      JUIXFXGXOXKCDEFGXLXLUJZXKUJZXOUJZMUKULXIYJYNXIYJYNXIYJPZYKIOZXNYLOZXQYLOZ
      PZYFUGZFUMZEUMZUGZJUMZYNYRUUFJXIYJUUFXIYSYJUUEXIYSYJUUEXIYSPZYJPZUUDEUUHY
      JEUUHEUNXMYIEXMEUNYHEXLUOUPUPUUIUUCFUUHYJFUUHFUNXMYIFXMFUNYHFEXLXNXLOZFUN
      YGFXLUOUQUPUPYJUUHUUCYIXMUUHUUCUGZYIUUJXQXLOZPZYGUGUUMYEPZYFUGZXMUUKUGYGE
      FXLXLURUUMYEYFUSUUHXMUUOUUCUUHXMUUOUUCUGUUHXMPZUUBUUNYFUUPUUBUUJXTBUEZXPY
      KUEZSZUULYCBUEZXRYKUEZSZPZUUNXCXEYSUUBUVCUGUUPXCXEYSSZYTUUSUUAUVBUVDYTUUS
      BYKXOXKDXNHXLIKYOYPYQLUTVAUVDUUAUVBBYKXOXKDXQHXLIKYOYPYQLUTVAVBXIXCYSXMXC
      XFXHVCVDXIXEYSXMXFXCXEXHXDXEVEVFVDXIYSXMVGVHUUPUVCUUMYEUVCUUMUGUUPUUSUUJU
      VBUULUUJUUQUURVCUULUUTUVAVCVIVJXIUVCYEUGZYSXMXCXFXHUVEXCXDXEXHUVEUGXCXDXE
      SXHYABUEZUVEABXODGHIKYQLVKUVFUVCYEUVFUVCPXSYBYDUVCXSUVFUUSUVBXSUURUUJUVBX
      SUGUUQUVBUURXSUVAUULUURXSUGZUUTUVGYKXRUURYKXRUEXSXPYKXRVLVMVNVOVPVOVQVRUV
      CUVFYBUUSUVFYBUGZUVBUUQUUJUVHUURUVFUUQYBUUQYBUGBYAUUQBYAUEZYBXTBYAVLVMVNV
      PVFVSVTUVCUVFYDUVBUVFYDUGZUUSUUTUULUVJUVAUVFUUTYDUUTYDUGBYAUUTUVIYDYCBYAV
      LVMVNVPVFVRVTWAWBWCWDWEVDWFWGWHWBWIWJVTVTWKWKWLWMVQWNYNUUEJIUHUUGYMUUEJIY
      FEFYLYLWOWPUUEJIWQWRWSWBABCDEFGHIJKLMWTXAXB $.
      $( [17-Nov-2008] $) $( [27-Oct-2008] $)
  $}

  $c cinv $.
  $( Extend class notation to include a function that returns the inverses of a
     morphism. $)
  ccinv $a class cinv $.

  ${
    $d x y f z g $.
    $( Function ( indexed by the category ` x ` ) returning the inverses of a
       morphism ` f ` . $)
    df-cinv $a |- cinv = { <. x , y >. | ( x e. Cat /\
      y = { <. f , z >. | ( f e. dom ( dom_ ` x ) /\
      z = { g e. dom ( dom_ ` x ) | ( ( f ( o_ ` x ) g ) =
      ( ( id_ ` x ) ` ( ( cod_ ` x ) ` f ) ) /\
     ( g ( o_ ` x ) f ) = ( ( id_ ` x ) ` ( ( dom_ ` x ) ` f ) ) ) } ) } ) } $.
  $}

  ${
    $d C x y z $.  $d D x y z $.  $d J x y z $.  $d M f g x y z $.
    $d R x y z $.  $d T f g x y z $.
    cinvlem1.1 $e |- M = dom ( dom_ ` T ) $.
    cinvlem1.2 $e |- D = ( dom_ ` T ) $.
    cinvlem1.3 $e |- C = ( cod_ ` T ) $.
    cinvlem1.4 $e |- R = ( o_ ` T ) $.
    cinvlem1.5 $e |- J = ( id_ ` T ) $.
    $( The set of the inverses of all the morphisms . $)
    cinvlem1 $p |- ( T e. Cat -> ( cinv ` T ) = { <. f , z >. | ( f e. M /\
      z = { g e. M | ( ( f R g ) = ( J ` ( C ` f ) )
      /\ ( g R f ) = ( J ` ( D ` f ) ) ) } ) } ) $=
      ( vx vy cv cdom_ cfv cdm wcel co_ co ccod_ cid_ wceq wa crab copab ccat
      ccinv fveq2 dmeqd syl6eqr eleq2d rabeq syl opreqd fveq1d fveq12d eqeq12d
      anbi12d rabbisdv eqtrd eqeq2d opabbidv df-cinv cvv fvex dmex eqeltri
      opabex2 fvopab4 ) OPEFQZOQZRSZTZUAZAQZVNGQZVOUBSZUCZVNVOUDSZSZVOUESZSZUFZ
      VTVNWAUCZVNVPSZWESZUFZUGZGVQUHZUFZUGZFAUIVNIUAZVSVNVTDUCZVNBSZHSZUFZVTVND
      UCZVNCSZHSZUFZUGZGIUHZUFZUGZFAUIUJUKVOEUFZWOXHFAXIVRWPWNXGXIVQIVNXIVQERSZ
      TZIXIVPXJVOERULZUMJUNZUOXIWMXFVSXIWMWLGIUHZXFXIVQIUFWMXNUFXMWLGVQIUPUQXIW
      LXEGIXIWGWTWKXDXIWBWQWFWSXIWADVNVTXIWAEUBSDVOEUBULMUNZURXIWDWRWEHXIWEEUES
      HVOEUEULNUNZXIVNWCBXIWCEUDSBVOEUDULLUNUSUTVAXIWHXAWJXCXIWADVTVNXOURXIWIXB
      WEHXPXIVNVPCXIVPXJCXLKUNUSUTVAVBVCVDVEVBVFOPAFGVGFAIXFIXKVHJXJERVIVJVKVLV
      M $.
      $( [22-Dec-2008] $) $( [22-Dec-2008] $)
  $}

  ${
    $d C f z $.  $d D f z $.  $d F f g z $.  $d J f z $.  $d M f g z $.
    $d R f z $.  $d T f g z $.
    cinvlem2.1 $e |- M = dom ( dom_ ` T ) $.
    cinvlem2.2 $e |- D = ( dom_ ` T ) $.
    cinvlem2.3 $e |- C = ( cod_ ` T ) $.
    cinvlem2.4 $e |- R = ( o_ ` T ) $.
    cinvlem2.5 $e |- J = ( id_ ` T ) $.
    cinvlem2.6 $e |- T e. Cat $.
    $( The set of the inverses of the morphism ` F ` . $)
    cinvlem2 $p |- ( F e. M -> ( ( cinv ` T ) ` F ) =
      { g e. M | ( ( F R g ) = ( J ` ( C ` F ) )
      /\ ( g R F ) = ( J ` ( D ` F ) ) ) } ) $=
      ( vf vz cv co_ cfv co wceq wa crab ccinv eqcomi a1i id eqidd opreq123d
      fveq2 fveq2d eqeq12d anbi12d rabbisdv ccat wcel copab eqid cinvlem1
      ax-mp cdom_ cdm cvv fvex dmex eqeltri rabex fvopab4 ) OPFOQZEQZDRSZTZVIAS
      ZGSZUAZVJVIVKTZVIBSZGSZUAZUBZEHUCZFVJCTZFASZGSZUAZVJFCTZFBSZGSZUAZUBZEHUC
      HDUDSZVIFUAZVTWJEHWLVOWEVSWIWLVLWBVNWDWLVIFVJVJVKCVKCUAWLCVKLUEUFZWLUGZWL
      VJUHZUIWLVMWCGVIFAUJUKULWLVPWFVRWHWLVJVJVIFVKCWMWOWNUIWLVQWGGVIFBUJUKULUM
      UNDUOUPWKVIHUPPQWAUAUBOPUQUANPABVKDOEGHIJKVKURMUSUTWJEHHDVASZVBVCIWPDVAVD
      VEVFVGVH $.
      $( [22-Dec-2008] $) $( [22-Dec-2008] $)
  $}

  ${
    $d C g $.  $d D g $.  $d F g $.  $d G g $.  $d J g $.  $d M g $.  $d R g $.
    $d T g $.
    cinvlem3.1 $e |- M = dom ( dom_ ` T ) $.
    cinvlem3.2 $e |- D = ( dom_ ` T ) $.
    cinvlem3.3 $e |- C = ( cod_ ` T ) $.
    cinvlem3.4 $e |- R = ( o_ ` T ) $.
    cinvlem3.5 $e |- J = ( id_ ` T ) $.
    cinvlem3.6 $e |- T e. Cat $.
    cinvlem3.7 $e |- F e. M $.
    $( The set of the inverses of the morphism ` F ` . $)
    cinvlem3 $p |-
      ( G e. ( ( cinv ` T ) ` F ) <->
      ( G e. M /\ ( F R G ) = ( J ` ( C ` F ) )
      /\ ( G R F ) = ( J ` ( D ` F ) ) ) ) $=
      ( vg cv co cfv wceq wa crab wcel ccinv w3a opreq2 eqeq1d opreq1 anbi12d
      elrab cinvlem2 ax-mp eleq2i 3anass 3bitr4i ) FEPQZCRZEASGSZTZUPECRZEBSGSZ
      TZUAZPHUBZUCFHUCZEFCRZURTZFECRZVATZUAZUAFEDUDSSZUCVEVGVIUEVCVJPFHUPFTZUSV
      GVBVIVLUQVFURUPFECUFUGVLUTVHVAUPFECUHUGUIUJVKVDFEHUCVKVDTOABCDPEGHIJKLMNU
      KULUMVEVGVIUNUO $.
      $( [22-Dec-2008] $) $( [22-Dec-2008] $)
  $}

