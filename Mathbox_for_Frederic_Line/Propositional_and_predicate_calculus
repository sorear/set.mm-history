$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Propositional and predicate calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ompfl3.1 $e |- ( ( ph /\ ps /\ ch ) -> ( th <-> ( ch /\ ta ) ) ) $.
    $( Remove a hypothesis from the second member of a biimplication. $)
    ompfl3 $p |- ( ( ph /\ ps /\ ch ) -> ( th <-> ta ) ) $=
      ( w3a wa wb ibar 3ad2ant3 bitr4d ) ABCGDCEHZEFCAEMIBCEJKL $.
      $( [22-Jul-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    intn3and.1 $e |- ( ph -> -. ps ) $.
    $( Introduction of a triple conjunct inside a contradiction. $)
    intn3an1d $p |- ( ph -> -. ( ps /\ ch /\ th ) ) $=
      ( wa wn w3a intnanrd df-3an notbii sylibr ) ABCFZDFZGBCDHZGAMDABCEIIONBCD
      JKL $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)

    $( Introduction of a triple conjunct inside a contradiction. $)
    intn3an2d $p |- ( ph -> -. ( ch /\ ps /\ th ) ) $=
      ( wa wn w3a intnand intnanrd df-3an notbii sylibr ) ACBFZDFZGCBDHZGANDABC
      EIJPOCBDKLM $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)

    $( Introduction of a triple conjunct inside a contradiction. $)
    intn3an3d $p |- ( ph -> -. ( ch /\ th /\ ps ) ) $=
      ( wa wn w3a intnand df-3an notbii sylibr ) ACDFZBFZGCDBHZGABMEIONCDBJKL
      $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)
  $}

  $( A consequence of ` /\ ` associativity in a triple conjunct. $)
  and4as $p |- ( ( ph /\ ps /\ ( ch /\ th ) ) <->
    ( ( ph /\ ps /\ ch ) /\ th ) ) $=
    ( wa w3a wi pm3.2 3exp imp4a 3imp id exp4a 3imp1 impbii ) ABCDEZFZABCFZDEZA
    BPSABCDSABCDSGRDHIJKABCDQABCDQABPQQLIMNO $.
    $( [14-Jul-2007] $) $( [14-Jul-2007] $)

  $( A consequence of ` /\ ` associativity in a triple conjunct. $)
  and4com $p |- ( ( ph /\ ( ps /\ ch /\ th ) ) <->
    ( ( ph /\ ps /\ ch ) /\ th ) ) $=
    ( w3a wa wi pm3.2 3exp 3imp2 3expd 3imp1 impbii ) ABCDEZFZABCEZDFZABCDQABCD
    QGPDHIJABCDOABCDOANHKLM $.
    $( [14-Jul-2007] $) $( [14-Jul-2007] $)

  ${ $v et $. $( Greek eta $)
    anddi2.wet $f wff et $.
    $( Conjunction of triple disjunctions. $)
    anddi2 $p |- ( ( ( ph \/ ps \/ ch ) /\ ( th \/ ta \/ et ) ) <->
              ( ( ( ph /\ th ) \/ ( ph /\ ta ) \/ ( ph /\ et ) ) \/
              ( ( ps /\ th ) \/ ( ps /\ ta ) \/ ( ps /\ et ) ) \/
              ( ( ch /\ th ) \/ ( ch /\ ta ) \/ ( ch /\ et ) ) ) ) $=
      ( w3o wa wo df-3or anbi12i anddi andir orbi12i andi orbi1i 3mix1 orcd
      3mix2 jaoi olcd 3mix3 sylibr orc olc 3jaoi impbii bitri 3bitri ) ABCGZDEF
      GZHABIZCIZDEIZFIZHULUNHZULFHZIZCUNHZCFHZIZIZADHZAEHZAFHZGZBDHZBEHZBFHZGZC
      DHZCEHZUTGZGZUJUMUKUOABCJDEFJKULCUNFLVBVCVDIZVGVHIZIZVEVIIZIZVKVLIZUTIZIZ
      VNURVSVAWAUPVQUQVRABDELABFMNUSVTUTCDEOPNWBVNWBVFVJIZVMIZVNVSWDWAVQWDVRVOW
      DVPVCWDVDVCWCVMVCVFVJVCVDVEQRRVDWCVMVDVFVJVDVCVESRRTVGWDVHVGWCVMVGVJVFVGV
      HVIQUARVHWCVMVHVJVFVHVGVISUARTTVEWDVIVEWCVMVEVFVJVEVCVDUBRRVIWCVMVIVJVFVI
      VGVHUBUARTTVTWDUTVKWDVLVKVMWCVKVLUTQUAVLVMWCVLVKUTSUATUTVMWCUTVKVLUBUATTV
      FVJVMJUCVFWBVJVMVCWBVDVEVCVSWAVCVQVRVCVOVPVCVDUDRRRVDVSWAVDVQVRVDVOVPVDVC
      UERRRVEVSWAVEVRVQVEVIUDUARUFVGWBVHVIVGVSWAVGVQVRVGVPVOVGVHUDUARRVHVSWAVHV
      QVRVHVPVOVHVGUEUARRVIVSWAVIVRVQVIVEUEUARUFVKWBVLUTVKWAVSVKVTUTVKVLUDRUAVL
      WAVSVLVTUTVLVKUERUAUTWAVSUTVTUEUAUFUFUGUHUI $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${
    $d w ps $.  $d x ps $.  $d y ps $.  $d z ps $.
    $( Theorem 19.41 of [Margaris] p. 90 with 4 quantifiers. $)
    19.41vvvv $p |- ( E. w E. x E. y E. z ( ph /\ ps ) <->
                     ( E. w E. x E. y E. z ph /\ ps ) ) $=
      ( wa wex 19.41vvv exbii 19.41v bitri ) ABGEHDHCHZFHAEHDHCHZBGZFHNFHBGMOFA
      BCDEIJNBFKL $.
      $( [14-Jul-2007] $) $( [14-Jul-2007] $)
  $}

  ${ $d ch w x z $.  $d ph x y $.  $d ph x z $.  $d ps w y $.
     $d ps w z $.  $d th w x y $.
    $( Rearrange existential quantifiers. $)
    eeeeanv $p |- ( E. w E. x E. y E. z ( ( ph /\ ps /\ ch ) /\ th ) <->
                 ( ( E. w ph /\ E. x ps /\ E. y ch ) /\ E. z th ) ) $=
      ( w3a wa wex 19.41vvv ancom eeeanv anbi2i 3bitri exbii 19.41v bitri
      and4com 3exbii 3bitr3i ) BCDIZAJZGKFKEKZHKZAHKZBEKZCFKZDGKZIZJZABCIDJZGKF
      KEKZHKUGUHUIIUJJUFAUKJZHKULUEUOHUEUCGKFKEKZAJAUPJUOUCAEFGLUPAMUPUKABCDEFG
      NOPQAUKHRSUEUNHUDUMEFGUDAUCJUMUCAMABCDTSUAQUGUHUIUJTUB $.
      $( [14-Jul-2007] $) $( [14-Jul-2007] $)
  $}

  ${ $d x A $.
    r19.3rzvb.1 $e |- ( ph -> A. x ph ) $.
    $( Restricted quantification of a proposition where the variable is not free.
       See ~ r19.3rzv . $)
    r19.3rzvb $p |- ( A =/= (/) -> ( ph <-> A. x e. A ph ) ) $=
      ( c0 wne cv wcel wex wi wral wb n0 biimt sylbi wal df-ral 19.23 bitri
      syl6bbr ) CEFZABGCHZBIZAJZABCKZUAUCAUDLBCMUCANOUEUBAJBPUDABCQUBABDRST $.
      $( [3-Jan-2008] $) $( [3-Jan-2008] $)
  $}

  ${
    pm2.01bd.1 $e |- ( ph -> ( -. ps -> ps ) ) $.
    $( Deduction based on reductio ad absurdum. $)
    pm2.01bd $p |- ( ph -> ps ) $=
      ( wn con3d pm2.01d notnot sylibr ) ABDZDBAIAIBCEFBGH $.
      $( [3-Aug-2009] $) $( [12-Jul-2009] $)
  $}

  $( Negation of a complex predicate calculus formula. $)
  negcmpprcal1 $p |- ( -. E. x e. A A. y e. B ( ph -> ps )
     <-> A. x e. A E. y e. B ( ph /\ -. ps ) ) $=
    ( wn wa wrex wral wi rexanali ralbii ralnex bitr2i ) ABGHDFIZCEJABKDFJZGZCE
    JQCEIGPRCEABDFLMQCENO $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  $( Negation of a complex predicated inequality. $)
  negcmpprcal2 $p |- ( -. E. x e. A A. y e. B C =/= D
    <-> A. x e. A E. y e. B C = D ) $=
    ( wne wral wn wrex wceq rexnal bicomi ralbii ralnex nne rexbii 3bitr3i ) EF
    GZBDHZIZACHSIZBDJZACHTACJIEFKZBDJZACHUAUCACUCUASBDLMNTACOUCUEACUBUDBDEFPQNR
    $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  ${
    sylan31c.1 $e |- ( ta -> ph ) $.
    sylan31c.2 $e |- ( ta -> ps ) $.
    sylan31c.3 $e |- ( ta -> ch ) $.
    ${
      sylan31c.4 $e |- ( ( ( ph /\ ps ) /\ ch ) -> th ) $.
      $( A syllogism inference combined with contraction. (Contributed by Jeff
         Hankins, 1-Aug-2009.) $)
      sylan31c $p |- ( ta -> th ) $=
        ( wa jca31 syl ) EABJCJDEABCFGHKIL $.
        $( [19-Aug-2009] $) $( [1-Aug-2009] $)
    $}

    ${
      sylan32c.4 $e |- ( ( ph /\ ( ps /\ ch ) ) -> th ) $.
      $( A syllogism inference combined with contraction. (Contributed by Jeff
         Hankins,  1-Aug-2009.) $)
      sylan32c $p |- ( ta -> th ) $=
        ( wa jca32 syl ) EABCJJDEABCFGHKIL $.
        $( [19-Aug-2009] $) $( [1-Aug-2009] $)
    $}
  $}

  ${
    sylanbrc.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    sylanbrc.2 $e |- ( th -> ps ) $.
    sylanbrc.3 $e |- ( th -> ch ) $.
    $( Syllogism inference. (Contributed by Jeff  Madsen 2-Sep-2009) $)
    sylanbrc $p |- ( th -> ph ) $=
      ( wa jca sylibr ) DBCHADBCFGIEJ $.
      $( [2-Sep-2009] $)
  $}


