$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Propositional and predicate calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    intn3and.1 $e |- ( ph -> -. ps ) $.
    $( Introduction of a triple conjunct inside a contradiction.  (Contributed
       by FL, 27-Dec-2007.)  (The proof was shortened by Andrew Salmon,
       26-Jun-2011.) $)
    intn3an1d $p |- ( ph -> -. ( ps /\ ch /\ th ) ) $=
      ( w3a simp1 nsyl3 con2i ) BCDFZAABJEBCDGHI $.
      $( [26-Jun-2011] $) $( [27-Dec-2007] $)

    $( Introduction of a triple conjunct inside a contradiction.  (Contributed
       by FL, 27-Dec-2007.)  (The proof was shortened by Andrew Salmon,
       26-Jun-2011.) $)
    intn3an2d $p |- ( ph -> -. ( ch /\ ps /\ th ) ) $=
      ( w3a simp2 nsyl3 con2i ) CBDFZAABJECBDGHI $.
      $( [26-Jun-2011] $) $( [27-Dec-2007] $)

    $( Introduction of a triple conjunct inside a contradiction.  (Contributed
       by FL, 27-Dec-2007.)  (The proof was shortened by Andrew Salmon,
       26-Jun-2011.) $)
    intn3an3d $p |- ( ph -> -. ( ch /\ th /\ ps ) ) $=
      ( w3a simp3 nsyl3 con2i ) CDBFZAABJECDBGHI $.
      $( [26-Jun-2011] $) $( [27-Dec-2007] $)
  $}

  $( A consequence of ` /\ ` associativity in a triple conjunct.  (Contributed
     by FL, 14-Jul-2007.) $)
  and4as $p |- ( ( ph /\ ps /\ ( ch /\ th ) ) <->
    ( ( ph /\ ps /\ ch ) /\ th ) ) $=
    ( wa w3a wi pm3.2 3exp imp4a 3imp id exp4a 3imp1 impbii ) ABCDEZFZABCFZDEZA
    BPSABCDSABCDSGRDHIJKABCDQABCDQABPQQLIMNO $.
    $( [14-Jul-2007] $)

  $( A consequence of ` /\ ` associativity in a triple conjunct.  (Contributed
     by FL, 14-Jul-2007.)  (The proof was shortened by Andrew Salmon,
     25-May-2011.) $)
  and4com $p |- ( ( ph /\ ( ps /\ ch /\ th ) ) <->
    ( ( ph /\ ps /\ ch ) /\ th ) ) $=
    ( w3a wa 3anass anbi2i and4as 3bitr2i ) ABCDEZFABCDFZFZFABLEABCEDFKMABCDGHA
    BLGABCDIJ $.
    $( [25-May-2011] $) $( [14-Jul-2007] $)

  ${
    $( Conjunction of triple disjunctions.  (Contributed by FL, 5-Jan-2009.)
       (The proof was shortened by Andrew Salmon, 25-May-2011.) $)
    anddi2 $p |- ( ( ( ph \/ ps \/ ch ) /\ ( th \/ ta \/ et ) ) <->
              ( ( ( ph /\ th ) \/ ( ph /\ ta ) \/ ( ph /\ et ) ) \/
              ( ( ps /\ th ) \/ ( ps /\ ta ) \/ ( ps /\ et ) ) \/
              ( ( ch /\ th ) \/ ( ch /\ ta ) \/ ( ch /\ et ) ) ) ) $=
      ( w3o wa wo df-3or anbi12i andir orbi1i andi orbi12i 3orbi123i 3bitr4ri
      3bitri ) ABCGZDEFGZHABIZCIZDEIZFIZHUAUDHZCUDHZIZADHZAEHZAFHZGZBDHZBEHZBFH
      ZGZCDHZCEHZCFHZGZGZSUBTUDABCJDEFJKUACUDLUGAUDHZBUDHZIZUFIAUCHZUJIZBUCHZUN
      IZIZCUCHZURIZIZUTUEVCUFABUDLMVCVHUFVJVAVEVBVGAUCFNBUCFNOCUCFNOUHUIIZUJIZU
      LUMIZUNIZUPUQIZURIZGVMVOIZVQIUTVKVMVOVQJUKVMUOVOUSVQUHUIUJJULUMUNJUPUQURJ
      PVHVRVJVQVEVMVGVOVDVLUJADENMVFVNUNBDENMOVIVPURCDENMOQRR $.
      $( [25-May-2011] $) $( [5-Jan-2009] $)
  $}

  ${
    condis.1 $e |- ( ph -> ps ) $.
    condis.2 $e |- ( -. ph -> ch ) $.
    $( Proof by contradiction combined with a disjunction.  (Contributed by FL,
       20-Apr-2011.) $)
    condis $p |- ( ps \/ ch ) $=
      ( wn wo exmid orim12i ax-mp ) AAFZGBCGAHABKCDEIJ $.
      $( [20-Apr-2011] $)
  $}

  ${
    condisd.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    condisd.2 $e |- ( ( ph /\ -. ps ) -> th ) $.
    $( Proof by contradiction combined with a disjunction.  (Contributed by FL,
       20-Apr-2011.) $)
    condisd $p |- ( ph -> ( ch \/ th ) ) $=
      ( wn wo exmid wa andi orim12i sylbi mpan2 ) ABBGZHZCDHZBIAPJABJZAOJZHQABO
      KRCSDEFLMN $.
      $( [20-Apr-2011] $)
  $}

  ${
    $d ch w x z $.  $d ph x y $.  $d ph x z $.  $d ps w y $.  $d ps w z $.
    $d th w x y $.
    $( Rearrange existential quantifiers.  (Contributed by FL, 14-Jul-2007.) $)
    eeeeanv $p |- ( E. w E. x E. y E. z ( ( ph /\ ps /\ ch ) /\ th ) <->
                 ( ( E. w ph /\ E. x ps /\ E. y ch ) /\ E. z th ) ) $=
      ( w3a wa wex 19.41vvv ancom eeeanv anbi2i 3bitri exbii 19.41v bitri
      and4com 3exbii 3bitr3i ) BCDIZAJZGKFKEKZHKZAHKZBEKZCFKZDGKZIZJZABCIDJZGKF
      KEKZHKUGUHUIIUJJUFAUKJZHKULUEUOHUEUCGKFKEKZAJAUPJUOUCAEFGLUPAMUPUKABCDEFG
      NOPQAUKHRSUEUNHUDUMEFGUDAUCJUMUCAMABCDTSUAQUGUHUIUJTUB $.
      $( [14-Jul-2007] $)
  $}

  $( Negation of a complex predicate calculus formula.  (Contributed by FL,
     31-Jul-2009.) $)
  negcmpprcal1 $p |- ( -. E. x e. A A. y e. B ( ph -> ps )
     <-> A. x e. A E. y e. B ( ph /\ -. ps ) ) $=
    ( wn wa wrex wral wi rexanali ralbii ralnex bitr2i ) ABGHDFIZCEJABKDFJZGZCE
    JQCEIGPRCEABDFLMQCENO $.
    $( [31-Jul-2009] $)

  $( Negation of a complex predicated inequality.  (Contributed by FL,
     31-Jul-2009.) $)
  negcmpprcal2 $p |- ( -. E. x e. A A. y e. B C =/= D
    <-> A. x e. A E. y e. B C = D ) $=
    ( wne wral wn wrex wceq rexnal bicomi ralbii ralnex nne rexbii 3bitr3i ) EF
    GZBDHZIZACHSIZBDJZACHTACJIEFKZBDJZACHUAUCACUCUASBDLMNTACOUCUEACUBUDBDEFPQNR
    $.
    $( [31-Jul-2009] $)

  $( Declare connectives for exclusive disjunction ('xor'). $)
  $c (+) $. $( Oplus (read:  'xor') $)

  $( Extend wff definition to include exclusive disjunction ('xor'). $)
  wxo $a wff ( ph (+) ps ) $.

  $( Define exclusive disjunction (logical 'xor'). $)
  df-xor $a |- ( ( ph (+) ps ) <-> -. ( ph <-> ps ) ) $.

  $( ` (+) ` is associative.  (Contributed by FL, 22-Nov-2010.)  (The proof was
     shortened by Andrew Salmon, 8-Jun-2011.) $)
  assxor $p |- ( ( ( ph (+) ps ) (+) ch ) <-> ( ph (+) ( ps (+) ch ) ) ) $=
    ( wxo wb wn biass notbii nbbn pm5.18 con2bii 3bitr4i df-xor bibi1i bibi2i
    ) ABDZCEZFABCDZEZFPCDARDQSABEZFZCEZABCEZFZEZQSTCEZFAUCEZFUBUEUFUGABCGHTCIUG
    UEAUCJKLPUACABMNRUDABCMOLHPCMARML $.
    $( [8-Jun-2011] $) $( [22-Nov-2010] $)

  $( This tautology shows that xor is really exclusive.  (Contributed by FL,
     22-Nov-2010.) $)
  excxor $p |- ( ( ph (+) ps ) <->
       ( ( ph /\ -. ps ) \/ ( -. ph /\ ps ) ) ) $=
    ( wxo wb wn wa wo df-xor xor ancom orbi2i 3bitri ) ABCABDEABEFZBAEZFZGMNBFZ
    GABHABIOPMBNJKL $.
    $( [22-Nov-2010] $)

  ${
    $d A y $.  $d B y $.  $d x y $.
    eqriv2.1 $e |- ( y e. A -> A. x y e. A ) $.
    eqriv2.2 $e |- ( y e. B -> A. x y e. B ) $.
    eqriv2.3 $e |- ( x e. A <-> x e. B ) $.
    $( Infer equality of classes from equivalence of membership.  (Contributed
       by FL, 22-Nov-2010.) $)
    eqriv2 $p |- A = B $=
      ( wceq cv wcel wb cleqf mpgbir ) CDHAIZCJNDJKAABCDEFLGM $.
      $( [22-Nov-2010] $)
  $}

  $( Alternate definition of true.  In fact any tautology is a definition of
     true.  (Contributed by FL, 23-Mar-2011.) $)
  altdftru $p |- ( T. <-> ( ph \/ -. ph ) ) $=
    ( wtru wb wn wo df-tru exmid a1i biidd impbii bitri ) BAACZAADEZAFLMMLAGHMA
    IJK $.
    $( [23-Mar-2011] $)

  $( A true antecedent can be removed.  (Contributed by FL, 16-Apr-2012.) $)
  trant $p |- ( ( T. -> ph ) <-> ph ) $=
    ( wtru wi wb tru pm5.5 ax-mp ) BBACADEBAFG $.
    $( [16-Apr-2012] $)

  $( Vacuous universal quantification is true.  (Contributed by FL,
     16-Apr-2012.) $)
  vutr $p |- ( T. <-> A. x e. (/) ph ) $=
    ( c0 wral wtru wb ral0 tru tbt mpbi bicomi ) ABCDZELLEFABGELHIJK $.
    $( [16-Apr-2012] $)

  $( True can be removed from a conjunction.  (Contributed by FL,
     20-Mar-2011.) $)
  trcrm $p |- ( ( T. /\ ph ) <-> ph ) $=
    ( wtru wa simpr wn wo orc altdftru sylibr ancri impbii ) BACABADABAAAEZFBAL
    GAHIJK $.
    $( [20-Mar-2011] $)

  $( True is not false.  (Contributed by FL, 20-Mar-2011.) $)
  tnf $p |- ( T. <-> -. F. ) $=
    ( wfal wtru df-fal con2bii ) ABCD $.
    $( [20-Mar-2011] $)

  $( False implies anything.  (Contributed by FL, 20-Mar-2011.) $)
  fampany $p |- ( F. -> ph ) $=
    ( wfal wtru wn df-fal wo altdftru notbii wa pm4.54 simpr sylbir sylbi ) BCD
    ZAENAADZFZDZACPAGHQOAIAAAJOAKLMM $.
    $( [20-Mar-2011] $)

  $( Anything implies true.  (Contributed by FL, 20-Mar-2011.) $)
  anymptr $p |- ( ph -> T. ) $=
    ( wn wo wtru orc altdftru sylibr ) AAABZCDAHEAFG $.
    $( [20-Mar-2011] $)

  $( False can be removed from a disjunction.  (Contributed by FL,
     20-Mar-2011.) $)
  facrm $p |- ( ( F. \/ ph ) <-> ph ) $=
    ( wfal wo fampany id jaoi olc impbii ) BACABAAADAEFABGH $.
    $( [20-Mar-2011] $)

  ${
    $( If ` ( ph \/ ps ) ` is true for all ` x ` and ` ps ` is not true for all
       ` x ` then ` ph ` is true for some ` x ` .  (Contributed by FL,
       20-Apr-2011.) $)
    fordisxex $p |- ( ( A. x e. A ( ph \/ ps ) /\ -. A. x e. A ps )
        -> E. x e. A ph ) $=
      ( wral wn wo wrex rexnal wa r19.29 pm5.61 simplbi reximi syl sylan2br )
      BCDEFABGZCDEZBFZCDHZACDHZBCDIRTJQSJZCDHUAQSCDKUBACDUBASABLMNOP $.
      $( [20-Apr-2011] $)
  $}

  ${
    $d A x $.
    fates.1 $e |- A e. B $.
    $( Equivalence of ` A. ` and ` E. ` in the case of quantifiers restricted
       to a singleton.  (Contributed by FL, 1-Jun-2011.) $)
    fates $p |- ( A. x e. { A } ph <-> E. x e. { A } ph ) $=
      ( cv csn wcel wi wal wa wex wral wrex wceq elisseti alexeq elsn imbi1i
      albii anbi1i exbii 3bitr4i df-ral df-rex ) BFZCGZHZAIZBJZUHAKZBLZABUGMABU
      GNUFCOZAIZBJUMAKZBLUJULABCCDEPQUIUNBUHUMABCRZSTUKUOBUHUMAUPUAUBUCABUGUDAB
      UGUEUC $.
      $( [1-Jun-2011] $)
  $}


  ${
    $d A a x $.  $d a ph $.
    $( Equivalence of ` A. ` and ` E. ` in the case of quantifiers restricted
       to a singleton.  (Contributed by FL, 1-Jun-2011.) $)
    fatesg $p |- ( A e. V -> ( A. x e. { A } ph <-> E. x e. { A } ph ) ) $=
      ( va cv csn wral wrex wb wceq sneq raleqdv rexeqdv bibi12d cvv visset
      fates vtoclg ) ABEFZGZHZABUAIZJABCGZHZABUDIZJECDTCKZUBUEUCUFUGABUAUDTCLZM
      UGABUAUDUHNOABTPEQRS $.
      $( [1-Jun-2011] $)
  $}

  ${
    $d A x $.  $d ps x $.
    r19.2zr.1 $e |- ( ph -> ps ) $.
    $( Quantifying a hypothesis with a universal restricted quantifier.
       (Contributed by FL, 19-Sep-2011.) $)
    r19.2zr $p |- ( ( A =/= (/) /\ A. x e. A ph ) -> ps ) $=
      ( c0 wne wral wa wrex r19.2z r19.23aivw syl ) DFGACDHIACDJBACDKABCDELM $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.  $d ph x $.
    $( Removing a universal restricted quantifier when the variable doesn't
       occur in the proposition.  (Contributed by FL, 19-Sep-2011.) $)
    r19.2zrr $p |- ( ( A =/= (/) /\ A. x e. A ph ) -> ph ) $=
      ( id r19.2zr ) AABCADE $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.
    r19.23aib.1 $e |- ( ps -> A. x ps ) $.
    r19.23aib.2 $e |- ( x e. A -> ( ph -> ps ) ) $.
    $( Removal of an universal restricted quantifier in an antecedent.  See
       also ~ reximdva0 .  (Contributed by FL, 19-Apr-2012.) $)
    r19.23aib $p |- ( ( A =/= (/) /\ A. x e. A ph ) -> ps ) $=
      ( c0 wne wral wrex wi r19.2zb r19.23ai imim2i sylbi imp ) DGHZACDIZBQRACD
      JZKRBKACDLSBRABCDEFMNOP $.
      $( [19-Apr-2012] $)
  $}

  ${
    $d A x $.  $d ps x $.
    eqint.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    eqint.2 $e |- ps $.
    eqint.3 $e |- ( ph -> A C_ x ) $.
    $( To prove that a set ` A ` is the finest one that has the property
       ` ph ` , prove that ` A ` is a part of all sets that has this property
       and that ` A ` has also that property.  (Contributed by FL,
       21-Apr-2012.) $)
    eqint $p |- ( A e. V -> A = |^| { x | ph } ) $=
      ( wcel cab cint wss cv wi ssintab mpgbir a1i intmin3 eqssd ) DEIZDACJKZDU
      ALZTUBADCMLNCACDOHPQABCDEFGRS $.
      $( [21-Apr-2012] $)
  $}

  ${
    $d A x $.  $d ch x $.  $d ph x $.
    eqintg.1 $e |- ( x = A -> ( ch <-> ps ) ) $.
    eqintg.2 $e |- ( ph -> ch ) $.
    eqintg.3 $e |- ( ( ph /\ ps ) -> A C_ x ) $.
    $( To prove that a set ` A ` is the finest one that has the property ` ph `
       prove that ` A ` is a part of all sets that has this property and that
       ` A ` has also that property.  (Contributed by FL, 15-Oct-2012.) $)
    eqintg $p |- ( ( ph /\ A e. V ) -> A = |^| { x | ps } ) $=
      ( wcel wa cab cint cv wss wi wal ex 19.21aiv adantr ssintab sylibr wceq
      wb simpr bicomd ax-gen elabgt sylancl mpbird intss1 syl eqssd ) AEFJZKZEB
      DLZMZUOBEDNZOZPZDQZEUQOAVAUNAUTDABUSIRSTBDEUAUBUOEUPJZUQEOUOVBCACUNHTUOUN
      UREUCZBCUDPZDQVBCUDAUNUEVDDVCCBGUFUGBCDEFUHUIUJEUPUKULUM $.
      $( [15-Oct-2012] $)
  $}

  ${
    r19.37.1 $e |- ( ph -> A. x ph ) $.
    $( Restricted version of one direction of Theorem 19.37 of [Margaris]
       p. 90.  (The other direction doesn't hold when ` A ` is empty.)
       (Contributed by FL, 13-May-2012.) $)
    r19.37 $p |- ( E. x e. A ( ph -> ps ) -> ( ph -> E. x e. A ps ) ) $=
      ( wi wrex wral r19.35 cv wcel ax-1 r19.21ai imim1i sylbi ) ABFCDGACDHZBCD
      GZFAQFABCDIAPQAACDEACJDKLMNO $.
      $( [13-May-2012] $)
  $}

  $( Restricted specialization.  (Contributed by FL, 4-Jun-2012.) $)
  ra42e $p |- ( ( x e. A /\ y e. B /\ ph ) -> E. x e. A E. y e. B ph ) $=
    ( cv wcel w3a wrex wa wex simp1 ra4e 3adant1 19.8a syl11anc df-rex sylibr
    ) BFDGZCFEGZAHZSACEIZJZBKZUBBDIUASUBUDSTALTAUBSACEMNUCBOPUBBDQR $.
    $( [4-Jun-2012] $)

  ${
    $d A x $.
    $( Two ways to express substitution of ` A ` for ` x ` in ` ph ` .
       (Contributed by FL, 4-Jun-2012.) $)
    alexeqd $p |- ( A e. V
      -> ( A. x ( x = A -> ph ) <-> E. x ( x = A /\ ph ) ) ) $=
      ( wcel cvv cv wceq wi wal wa wex wb elisset c0 cif eqeq2 imbi1d albidv
      anbi1d exbidv bibi12d 0ex elimel alexeq dedth syl ) CDECFEZBGZCHZAIZBJZUJ
      AKZBLZMZCDNUHUOUIUHCOPZHZAIZBJZUQAKZBLZMCOCUPHZULUSUNVAVBUKURBVBUJUQACUPU
      IQZRSVBUMUTBVBUJUQAVCTUAUBABUPCOFUCUDUEUFUG $.
      $( [4-Jun-2012] $)
  $}


  ${
    $d A x $.  $d A z $.  $d B w $.  $d B x y $.  $d B z y $.  $d ch w $.
    $d ch x $.  $d ph z $.  $d ps y $.
    cbvrex2v.1 $e |- ( x = z -> ( ph <-> ch ) ) $.
    cbvrex2v.2 $e |- ( y = w -> ( ch <-> ps ) ) $.
    $( Change bound variables of double restricted universal quantification,
       using implicit substitution.  (Contributed by FL, 2-Jul-2012.) $)
    cbvrex2v $p |- ( E. x e. A E. y e. B ph <-> E. z e. A E. w e. B ps ) $=
      ( wrex weq rexbidv cbvrexv rexbii bitri ) AEILZDHLCEILZFHLBGILZFHLRSDFHDF
      MACEIJNOSTFHCBEGIKOPQ $.
      $( [2-Jul-2012] $)
  $}


  ${
    $d A x y $.  $d B y $.  $d C x $.  $d D x y $.  $d ch y $.  $d ph x y $.
    $d th x $.
    rcla42edv.1 $e |- ( x = A -> ( ps <-> th ) ) $.
    rcla42edv.2 $e |- ( y = B -> ( th <-> ch ) ) $.
    $( 2-variable restricted existential specialization, using implicit
       substitution.  ( ~ rcla42ev with an antecedent.)  (Contributed by FL,
       2-Jul-2012.) $)
    rcla42edv $p |- ( ( ph /\ A e. C /\ B e. D )
        -> ( ch -> E. x e. C E. y e. D ps ) ) $=
      ( wcel wrex wi wa w3a cv wceq anbi2d rcla42ev simpr reximi syl 3expia
      exp4b com3r 3imp ) AGIMZHJMZCBFJNZEINZOZUIUJAUMUIUJACULUIUJACPZULUIUJUNQA
      BPZFJNZEINULUOUNADPEFGHIJERGSBDAKTFRHSDCALTUAUPUKEIUOBFJABUBUCUCUDUEUFUGU
      H $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d ph y $.  $d x y $.  $d y A $.  $d y B $.  $d y C $.
    mpteq2dvg.1 $e |- ( ph -> A. x ph ) $.
    mpteq2dvg.2 $e |- ( ( ph /\ x e. A ) -> B = C ) $.
    $( Slightly more general equality inference for the maps to notation.
       (Contributed by FL, 14-Sep-2013.) $)
    mpteq2dvg $p |- ( ph -> ( x e. A |-> B ) = ( x e. A |-> C ) ) $=
      ( vy cv wcel wceq wa copab cmpt ax-17 eqeq2d pm5.32da opabbid df-mpt
      3eqtr4g ) ABICJZHIZDKZLZBHMUAUBEKZLZBHMBCDNBCENAUDUFBHFAHOAUAUCUEAUALDEUB
      GPQRBHCDSBHCEST $.
      $( [14-Sep-2013] $)
  $}

  ${
    pm11.53g.1 $e |- ( ph -> A. y ph ) $.
    pm11.53g.2 $e |- ( ps -> A. x ps ) $.
    $( Theorem *11.53 in [WhiteheadRussell] p. 164.  (Contributed by FL,
       27-Oct-2013.) $)
    pm11.53g $p |- ( A. x A. y ( ph -> ps ) <-> ( E. x ph -> A. y ps ) ) $=
      ( wi wal wex 19.21 albii hbal 19.23 bitri ) ABGDHZCHABDHZGZCHACIPGOQCABDE
      JKAPCBCDFLMN $.
      $( [6-Nov-2013] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.
    eqvinopb.1 $e |- B e. _V $.
    eqvinopb.2 $e |- C e. _V $.
    eqvinopb.3 $e |- D e. _V $.
    $( A variable introduction law for ordered triples.  See ~ eqvinop .
       (Contributed by FL, 6-Nov-2013.)  NEW $)
    eqvinopb $p |- ( A = <. <. B , C >. , D >. <-> E. x E. y E. z
      ( A = <. <. x , y >. , z >. /\
                  <. <. x , y >. , z >. = <. <. B , C >. , D >. ) ) $=
      ( cv cop wceq wa wex w3a cvv wcel wb visset otthg mp2an anbi2i ancom
      df-3an anbi1i anass bicomi bitri 3bitri exbii 19.42v 3ancoma 3anass
      opeq2 eqeq2d anbi2d ceqsexv opeq1d opeq1 bitr2i ) DAKZBKZLZCKZLZMZVFEFLZG
      LZMZNZCOZBOZAOVBEMZDVBFLZGLZMZNZAODVIMZVMVRAVMVNVCFMZDVDGLZMZNZNZBOVNWCBO
      ZNVRVLWDBVLVNVTVEGMZVGPZNZCOVNWGCOZNWDVKWHCVKVGVNVTWFPZNWJVGNZWHVJWJVGFQR
      GQRVJWJSIJVBVCEFVEGQQATBTCTUAUBUCVGWJUDWKVNVTNWFNZVGNVNVTWFNZNZVGNZWHWJWL
      VGVNVTWFUEUFWLWNVGVNVTWFUGUFWOVNWMVGNZNWHVNWMVGUGWPWGVNWGWPVTWFVGUEUHUCUI
      UJUJUKVNWGCULWIWCVNWIWFVTVGNZNZCOWCWGWRCWGWFVTVGPWRVTWFVGUMWFVTVGUNUIUKWQ
      WCCGJWFVGWBVTWFVFWADVEGVDUOUPUQURUIUCUJUKVNWCBULWEVQVNWBVQBFIVTWAVPDVTVDV
      OGVCFVBUOUSUPURUCUJUKVQVSAEHVNVPVIDVNVOVHGVBEFUTUSUPURVA $.
      $( [6-Nov-2013] $)
  $}

  ${
    $d ph w v u $.  $d x y z w v u A $.
    $( Substitution of class ` A ` for ordered triple
       ` <. <. x , y >. , z >. ` .  See ~ copsexg .  (Contributed by FL,
       10-Nov-2013.)  NEW $)
    copsexgb $p |- ( A = <. <. x , y >. , z >. ->
                  ( ph <-> E. x E. y E. z
                    ( A = <. <. x , y >. , z >. /\ ph ) ) ) $=
      ( vu vv vw cv cop wceq wa wex wb wi visset eqvinopb 19.8a 3syl ex w3a
      cvv wcel otthg mp2an anbi1i 3exbii hbe1 wal hbae simpl1 simpl2 simpl3
      simpr jca jca32 eximi 19.40 id 19.23aiv a1i anim2d eximd biidd drex1
      sylibd wn hbnae dveeq2 19.9d anim1d syl56 pm2.61i anim12d com12 simpl
      simprl simprrl 3jca simprrr impbii exbii 19.42v anbi2i 3bitri a17d syl5
      syl5bi syl6 19.23ai weu euequ1 equcom eubii mpbi eupick mpan sylan9
      3impa sylbi impbid eqeq1 anbi1d 3exbidv bibi2d imbi12d mpbiri adantr
      19.23aivv pm2.43i ) EBIZCIZJDIZJZKZAXOALZDMCMBMZNZXOEFIZGIZJHIZJZKZYBXNKZ
      LZHMZGMFMXOXROZFGHEXKXLXMBPCPZDPZQYFYGFGYEYGHYCYGYDYCYGYDAYDALZDMZCMZBMZN
      ZOYDAYMYDAYMYJYKYLYMYJDRYKCRYLBRSTYDXSXKKZXTXLKZYAXMKZUAZYMAOXLUBUCXMUBUC
      YDYRNYHYIXSXTXKXLYAXMUBUBFPGPHPUDUEZYMYRALZDMZCMZBMZYRAYJYTBCDYDYRAYSUFUG
      UUCYOYPYQALZDMZLZCMZLZBMZYRAUUBUUIBUUHBUHXLXKKCUIZUUBUUIOUUJUUBUUHCMUUIUU
      JUUAUUHCCBCUJUUAUUJUUHUUAYOYPUUDLZLZDMZYODMZUUKDMZLZUUJUUHOYTUULDYTYOYPUU
      DYOYPYQAUKYOYPYQAULYTYQAYOYPYQAUMYRAUNUOUPZUQYOUUKDURUUJUUPUUHUUJUUNYOUUO
      UUGUUNYOOUUJYOYODYOUSUTVAUUOUUGOZUUJXMXLKDUIZUURUUSUUOUUFDMUUGUUSUUKUUFDD
      CDUJUUSUUDUUEYPUUDUUEOUUSUUDDRVAVBVCUUFUUFDCUUSUUFVDVEVFUUOYPDMZUUELUUSVG
      ZUUFUUGYPUUDDURUVAUUTYPUUEYPUVADDCDVHDCGVIVJVKUUFCRVLVMVAVNVOSVOVCUUHUUHC
      BUUJUUHVDVEVFUUJVGZUUBUUHUUIUUBYOUUFLZCMZUVBUUHUUAUVCCUUAUUMYOUUOLUVCYTUU
      LDYTUULUUQUULYRAUULYOYPYQYOUUKVPYOYPUUDVQYOYPYQAVRVSYOYPYQAVTUOWAWBYOUUKD
      WCUUOUUFYOYPUUDDWCWDWEWBUVDYOCMZUUGLUVBUUHYOUUFCURUVBUVEYOUUGYOUVBCCBCVHU
      VBYOCWFVJVKWGWHUUHBRWIVMWJYOYPYQUUIAOYOYPLUUIUUEYQAYOUUIUUGYPUUEUUIYOUUGY
      OBWKZUUIYOUUGOXKXSKZBWKUVFBFWLUVGYOBBFWMWNWOYOUUGBWPWQVOUUGYPUUEYPCWKZUUG
      YPUUEOXLXTKZCWKUVHCGWLUVIYPCCGWMWNWOYPUUECWPWQVOWRUUEYQAYQDWKZUUEYQAOXMYA
      KZDWKUVJDHWLUVKYQDDHWMWNWOYQADWPWQVOWRWSWGWHWTXAYCXOYDXRYNEYBXNXBZYCXQYMA
      YCXPYJBCDYCXOYDAUVLXCXDXEXFXGXHUTXIWTXJ $.
      $( [10-Nov-2013] $) $( [6-Nov-2013] $)
  $}

