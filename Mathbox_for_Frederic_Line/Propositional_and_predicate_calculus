$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Propositional and predicate calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    intn3and.1 $e |- ( ph -> -. ps ) $.
    $( Introduction of a triple conjunct inside a contradiction.  (The proof
       was shortened by Andrew Salmon, 26-Jun-2011.) $)
    intn3an1d $p |- ( ph -> -. ( ps /\ ch /\ th ) ) $=
      ( w3a simp1 nsyl3 con2i ) BCDFZAABJEBCDGHI $.
      $( [26-Jun-2011] $) $( [27-Dec-2007] $)

    $( Introduction of a triple conjunct inside a contradiction.  (The proof
       was shortened by Andrew Salmon, 26-Jun-2011.) $)
    intn3an2d $p |- ( ph -> -. ( ch /\ ps /\ th ) ) $=
      ( w3a simp2 nsyl3 con2i ) CBDFZAABJECBDGHI $.
      $( [26-Jun-2011] $) $( [27-Dec-2007] $)

    $( Introduction of a triple conjunct inside a contradiction.  (The proof
       was shortened by Andrew Salmon, 26-Jun-2011.) $)
    intn3an3d $p |- ( ph -> -. ( ch /\ th /\ ps ) ) $=
      ( w3a simp3 nsyl3 con2i ) CDBFZAABJECDBGHI $.
      $( [26-Jun-2011] $) $( [27-Dec-2007] $)
  $}

  $( A consequence of ` /\ ` associativity in a triple conjunct. $)
  and4as $p |- ( ( ph /\ ps /\ ( ch /\ th ) ) <->
    ( ( ph /\ ps /\ ch ) /\ th ) ) $=
    ( wa w3a wi pm3.2 3exp imp4a 3imp id exp4a 3imp1 impbii ) ABCDEZFZABCFZDEZA
    BPSABCDSABCDSGRDHIJKABCDQABCDQABPQQLIMNO $.
    $( [14-Jul-2007] $)

  $( A consequence of ` /\ ` associativity in a triple conjunct.  (The proof
     was shortened by Andrew Salmon, 25-May-2011.) $)
  and4com $p |- ( ( ph /\ ( ps /\ ch /\ th ) ) <->
    ( ( ph /\ ps /\ ch ) /\ th ) ) $=
    ( w3a wa 3anass anbi2i and4as 3bitr2i ) ABCDEZFABCDFZFZFABLEABCEDFKMABCDGHA
    BLGABCDIJ $.
    $( [25-May-2011] $) $( [14-Jul-2007] $)

  ${
    $( Conjunction of triple disjunctions.  (The proof was shortened by Andrew
       Salmon, 25-May-2011.) $)
    anddi2 $p |- ( ( ( ph \/ ps \/ ch ) /\ ( th \/ ta \/ et ) ) <->
              ( ( ( ph /\ th ) \/ ( ph /\ ta ) \/ ( ph /\ et ) ) \/
              ( ( ps /\ th ) \/ ( ps /\ ta ) \/ ( ps /\ et ) ) \/
              ( ( ch /\ th ) \/ ( ch /\ ta ) \/ ( ch /\ et ) ) ) ) $=
      ( w3o wa wo df-3or anbi12i andir orbi1i andi orbi12i 3orbi123i 3bitr4ri
      3bitri ) ABCGZDEFGZHABIZCIZDEIZFIZHUAUDHZCUDHZIZADHZAEHZAFHZGZBDHZBEHZBFH
      ZGZCDHZCEHZCFHZGZGZSUBTUDABCJDEFJKUACUDLUGAUDHZBUDHZIZUFIAUCHZUJIZBUCHZUN
      IZIZCUCHZURIZIZUTUEVCUFABUDLMVCVHUFVJVAVEVBVGAUCFNBUCFNOCUCFNOUHUIIZUJIZU
      LUMIZUNIZUPUQIZURIZGVMVOIZVQIUTVKVMVOVQJUKVMUOVOUSVQUHUIUJJULUMUNJUPUQURJ
      PVHVRVJVQVEVMVGVOVDVLUJADENMVFVNUNBDENMOVIVPURCDENMOQRR $.
      $( [25-May-2011] $) $( [5-Jan-2009] $)
  $}

  ${
    condis.1 $e |- ( ph -> ps ) $.
    condis.2 $e |- ( -. ph -> ch ) $.
    $( Proof by contradiction combined with a disjunction. $)
    condis $p |- ( ps \/ ch ) $=
      ( wn wo exmid orim12i ax-mp ) AAFZGBCGAHABKCDEIJ $.
      $( [20-Apr-2011] $)
  $}

  ${
    condisd.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    condisd.2 $e |- ( ( ph /\ -. ps ) -> th ) $.
    $( Proof by contradiction combined with a disjunction. $)
    condisd $p |- ( ph -> ( ch \/ th ) ) $=
      ( wn wo exmid wa andi orim12i sylbi mpan2 ) ABBGZHZCDHZBIAPJABJZAOJZHQABO
      KRCSDEFLMN $.
      $( [20-Apr-2011] $)
  $}


  ${
    $d w ps $.  $d x ps $.  $d y ps $.  $d z ps $.
    $( Theorem 19.41 of [Margaris] p. 90 with 4 quantifiers. $)
    19.41vvvv $p |- ( E. w E. x E. y E. z ( ph /\ ps ) <->
                     ( E. w E. x E. y E. z ph /\ ps ) ) $=
      ( wa wex 19.41vvv exbii 19.41v bitri ) ABGEHDHCHZFHAEHDHCHZBGZFHNFHBGMOFA
      BCDEIJNBFKL $.
      $( [14-Jul-2007] $)
  $}

  ${
    $d ch w x z $.  $d ph x y $.  $d ph x z $.  $d ps w y $.  $d ps w z $.
    $d th w x y $.
    $( Rearrange existential quantifiers. $)
    eeeeanv $p |- ( E. w E. x E. y E. z ( ( ph /\ ps /\ ch ) /\ th ) <->
                 ( ( E. w ph /\ E. x ps /\ E. y ch ) /\ E. z th ) ) $=
      ( w3a wa wex 19.41vvv ancom eeeanv anbi2i 3bitri exbii 19.41v bitri
      and4com 3exbii 3bitr3i ) BCDIZAJZGKFKEKZHKZAHKZBEKZCFKZDGKZIZJZABCIDJZGKF
      KEKZHKUGUHUIIUJJUFAUKJZHKULUEUOHUEUCGKFKEKZAJAUPJUOUCAEFGLUPAMUPUKABCDEFG
      NOPQAUKHRSUEUNHUDUMEFGUDAUCJUMUCAMABCDTSUAQUGUHUIUJTUB $.
      $( [14-Jul-2007] $)
  $}

  ${
    $d x A $.
    r19.3rzvb.1 $e |- ( ph -> A. x ph ) $.
    $( Restricted quantification of a proposition where the variable is not
       free.  See ~ r19.3rzv . $)
    r19.3rzvb $p |- ( A =/= (/) -> ( ph <-> A. x e. A ph ) ) $=
      ( c0 wne cv wcel wex wi wral wb n0 biimt sylbi wal df-ral 19.23 bitri
      syl6bbr ) CEFZABGCHZBIZAJZABCKZUAUCAUDLBCMUCANOUEUBAJBPUDABCQUBABDRST $.
      $( [3-Jan-2008] $)
  $}

  ${
    pm2.01bd.1 $e |- ( ph -> ( -. ps -> ps ) ) $.
    $( Deduction based on reductio ad absurdum.  (The proof was shortened by
       Andrew Salmon, 7-May-2011.) $)
    pm2.01bd $p |- ( ph -> ps ) $=
      ( wn wi pm2.18 syl ) ABDBEBCBFG $.
      $( [12-May-2011] $) $( [12-Jul-2009] $)
  $}

  $( Negation of a complex predicate calculus formula. $)
  negcmpprcal1 $p |- ( -. E. x e. A A. y e. B ( ph -> ps )
     <-> A. x e. A E. y e. B ( ph /\ -. ps ) ) $=
    ( wn wa wrex wral wi rexanali ralbii ralnex bitr2i ) ABGHDFIZCEJABKDFJZGZCE
    JQCEIGPRCEABDFLMQCENO $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  $( Negation of a complex predicated inequality. $)
  negcmpprcal2 $p |- ( -. E. x e. A A. y e. B C =/= D
    <-> A. x e. A E. y e. B C = D ) $=
    ( wne wral wn wrex wceq rexnal bicomi ralbii ralnex nne rexbii 3bitr3i ) EF
    GZBDHZIZACHSIZBDJZACHTACJIEFKZBDJZACHUAUCACUCUASBDLMNTACOUCUEACUBUDBDEFPQNR
    $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  $( Declare connectives for exclusive disjunction ('xor'). $)
  $c (+) $. $( Oplus (read:  'xor') $)

  $( Extend wff definition to include exclusive disjunction ('xor'). $)
  wxo $a wff ( ph (+) ps ) $.

  $( Define exclusive disjunction (logical 'xor'). $)
  df-xor $a |- ( ( ph (+) ps ) <-> -. ( ph <-> ps ) ) $.

  $( ` (+) ` is associative.  (The proof was shortened by Andrew Salmon,
     8-Jun-2011.) $)
  assxor $p |- ( ( ( ph (+) ps ) (+) ch ) <-> ( ph (+) ( ps (+) ch ) ) ) $=
    ( wxo wb wn biass notbii nbbn pm5.18 con2bii 3bitr4i df-xor bibi1i bibi2i
    ) ABDZCEZFABCDZEZFPCDARDQSABEZFZCEZABCEZFZEZQSTCEZFAUCEZFUBUEUFUGABCGHTCIUG
    UEAUCJKLPUACABMNRUDABCMOLHPCMARML $.
    $( [8-Jun-2011] $) $( [22-Nov-2010] $)

  $( This tautology shows that xor is really exclusive. $)
  excxor $p |- ( ( ph (+) ps ) <->
       ( ( ph /\ -. ps ) \/ ( -. ph /\ ps ) ) ) $=
    ( wxo wb wn wa wo df-xor xor ancom orbi2i 3bitri ) ABCABDEABEFZBAEZFZGMNBFZ
    GABHABIOPMBNJKL $.
    $( [22-Nov-2010] $)

  ${
    $d A y $.  $d B y $.  $d x y $.
    eqriv2.1 $e |- ( y e. A -> A. x y e. A ) $.
    eqriv2.2 $e |- ( y e. B -> A. x y e. B ) $.
    eqriv2.3 $e |- ( x e. A <-> x e. B ) $.
    $( Infer equality of classes from equivalence of membership. $)
    eqriv2 $p |- A = B $=
      ( wceq cv wcel wb cleqf mpgbir ) CDHAIZCJNDJKAABCDEFLGM $.
      $( [22-Nov-2010] $)
  $}

  $( Theorem 19.26 of [Margaris] p. 90 with restricted quantifiers and three
     propositional variables. $)
  r19.26t $p |- ( A. x e. A ( ph /\ ps /\ ch ) <->
               ( A. x e. A ph /\ A. x e. A ps /\ A. x e. A ch ) ) $=
    ( w3a wral wa df-3an ralbii r19.26 anbi1i bitr4i 3bitri ) ABCFZDEGABHZCHZDE
    GPDEGZCDEGZHZADEGZBDEGZSFZOQDEABCIJPCDEKTUAUBHZSHUCRUDSABDEKLUAUBSIMN $.
    $( [22-Nov-2010] $)

  $( Alternate definition of true.  In fact any tautology is a definition of
     true. $)
  altdftru $p |- ( T. <-> ( ph \/ -. ph ) ) $=
    ( wtru wb wn wo df-tru exmid a1i biidd impbii bitri ) BAACZAADEZAFLMMLAGHMA
    IJK $.
    $( [23-Mar-2011] $)

  $( A true antecedent can be removed. $)
  trant $p |- ( ( T. -> ph ) <-> ph ) $=
    ( wtru wi wb tru pm5.5 ax-mp ) BBACADEBAFG $.
    $( [16-Apr-2012] $)

  $( Vacuous universal quantification is true. $)
  vutr $p |- ( T. <-> A. x e. (/) ph ) $=
    ( c0 wral wtru wb ral0 truvar mpbi bicomi ) ABCDZEKKEFABGKHIJ $.
    $( [16-Apr-2012] $)

  $( True can be removed from a conjunction. $)
  trcrm $p |- ( ( T. /\ ph ) <-> ph ) $=
    ( wtru wa simpr wn wo orc altdftru sylibr ancri impbii ) BACABADABAAAEZFBAL
    GAHIJK $.
    $( [20-Mar-2011] $)

  $( True is not false. $)
  tnf $p |- ( T. <-> -. F. ) $=
    ( wfal wtru df-fal con2bii ) ABCD $.
    $( [20-Mar-2011] $)

  $( False implies anything. $)
  fampany $p |- ( F. -> ph ) $=
    ( wfal wtru wn df-fal wo altdftru notbii wa pm4.54 simpr sylbir sylbi ) BCD
    ZAENAADZFZDZACPAGHQOAIAAAJOAKLMM $.
    $( [20-Mar-2011] $)

  $( Anything implies true. $)
  anymptr $p |- ( ph -> T. ) $=
    ( wn wo wtru orc altdftru sylibr ) AAABZCDAHEAFG $.
    $( [20-Mar-2011] $)

  $( False can be removed from a disjunction. $)
  facrm $p |- ( ( F. \/ ph ) <-> ph ) $=
    ( wfal wo fampany id jaoi olc impbii ) BACABAAADAEFABGH $.
    $( [20-Mar-2011] $)

  ${
    $( If ` ( ph \/ ps ) ` is true for all ` x ` and ` ps ` is not true for all
       ` x ` then ` ph ` is true for some ` x ` . $)
    fordisxex $p |- ( ( A. x e. A ( ph \/ ps ) /\ -. A. x e. A ps )
        -> E. x e. A ph ) $=
      ( wo wral wn wrex wa r19.29 pm5.61 simplbi reximi syl rexnal sylan2br )
      ABEZCDFZBGZCDHZACDHZBCDFGRTIQSIZCDHUAQSCDJUBACDUBASABKLMNBCDOP $.
      $( [20-Apr-2011] $)
  $}

  ${
    $d A x $.
    fates.1 $e |- A e. B $.
    $( Equivalence of ` A. ` and ` E. ` in the case of quantifiers restricted
       to a singleton. $)
    fates $p |- ( A. x e. { A } ph <-> E. x e. { A } ph ) $=
      ( cv csn wcel wi wal wa wex wral wrex wceq elisseti alexeq elsn imbi1i
      albii anbi1i exbii 3bitr4i df-ral df-rex ) BFZCGZHZAIZBJZUHAKZBLZABUGMABU
      GNUFCOZAIZBJUMAKZBLUJULABCCDEPQUIUNBUHUMABCRZSTUKUOBUHUMAUPUAUBUCABUGUDAB
      UGUEUC $.
      $( [1-Jun-2011] $)
  $}


  ${
    $d A a x $.  $d a ph $.
    $( Equivalence of ` A. ` and ` E. ` in the case of quantifiers restricted
       to a singleton. $)
    fatesg $p |- ( A e. B -> ( A. x e. { A } ph <-> E. x e. { A } ph ) ) $=
      ( va cv csn wral wrex wb wceq sneq raleqdv rexeqdv bibi12d cvv visset
      fates vtoclg ) ABEFZGZHZABUAIZJABCGZHZABUDIZJECDTCKZUBUEUCUFUGABUAUDTCLZM
      UGABUAUDUHNOABTPEQRS $.
      $( [1-Jun-2011] $)
  $}

  ${
    $d ps x $.
    r19.23aivr.1 $e |- ( ph -> ps ) $.
    $( Quantifying a hypothesis with an existential restricted quantifier. $)
    r19.23aivr $p |- ( E. x e. A ph -> ps ) $=
      ( wi cv wcel a1i r19.23aiv ) ABCDABFCGDHEIJ $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.  $d ps x $.
    r19.2zr.1 $e |- ( ph -> ps ) $.
    $( Quantifying a hypothesis with a universal restricted quantifier. $)
    r19.2zr $p |- ( ( A =/= (/) /\ A. x e. A ph ) -> ps ) $=
      ( c0 wne wral wa wrex r19.2z r19.23aivr syl ) DFGACDHIACDJBACDKABCDELM $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.  $d ph x $.
    $( Removing a universal restricted quantifier when the variable doesn't
       occur in the proposition. $)
    r19.2zrr $p |- ( ( A =/= (/) /\ A. x e. A ph ) -> ph ) $=
      ( id r19.2zr ) AABCADE $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.
    r19.23aib.1 $e |- ( ps -> A. x ps ) $.
    r19.23aib.2 $e |- ( x e. A -> ( ph -> ps ) ) $.
    $( Removal of an universal restricted quantifier in an antecedent.  See
       also ~ reximdva0 . $)
    r19.23aib $p |- ( ( A =/= (/) /\ A. x e. A ph ) -> ps ) $=
      ( c0 wne wral wrex wi r19.2zb r19.23ai imim2i sylbi imp ) DGHZACDIZBQRACD
      JZKRBKACDLSBRABCDEFMNOP $.
      $( [19-Apr-2012] $)
  $}

  ${
    $d A x $.  $d ps x $.
    eqint.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    eqint.2 $e |- ps $.
    eqint.3 $e |- ( ph -> A C_ x ) $.
    $( To prove that a set ` A ` is the finest one that has the property
       ` ph ` , prove that ` A ` is a part of all sets that has this property
       and that ` A ` has also that property. $)
    eqint $p |- ( A e. B -> A = |^| { x | ph } ) $=
      ( wcel cab cint wss cv wi ssintab mpgbir a1i intmin3 eqssd ) DEIZDACJKZDU
      ALZTUBADCMLNCACDOHPQABCDEFGRS $.
      $( [21-Apr-2012] $)
  $}

  ${
    $d A x $.  $d ch x $.  $d ph x $.
    eqintg.1 $e |- ( x = A -> ( ch <-> ps ) ) $.
    eqintg.2 $e |- ( ph -> ch ) $.
    eqintg.3 $e |- ( ( ph /\ ps ) -> A C_ x ) $.
    $( To prove that a set ` A ` is the finest one that has the property ` ph `
       prove that ` A ` is a part of all sets that has this property and that
       ` A ` has also that property. $)
    eqintg $p |- ( ( ph /\ A e. B ) -> A = |^| { x | ps } ) $=
      ( wcel wa cab cint cv wss wi wal ex 19.21aiv adantr ssintab sylibr wceq
      wb elabgt simpr bicomd ax-gen sylancl mpbird intss1 syl eqssd ) AEFJZKZEB
      DLZMZUOBEDNZOZPZDQZEUQOAVAUNAUTDABUSIRSTBDEUAUBUOEUPJZUQEOUOVBCACUNHTUNUR
      EUCZBCUDPZDQVBCUDUOBCDEFUEAUNUFVDDVCCBGUGUHUIUJEUPUKULUM $.
      $( [15-Oct-2012] $)
  $}

  ${
    r19.37.1 $e |- ( ph -> A. x ph ) $.
    $( Restricted version of one direction of Theorem 19.37 of [Margaris]
       p. 90.  (The other direction doesn't hold when ` A ` is empty.) $)
    r19.37 $p |- ( E. x e. A ( ph -> ps ) -> ( ph -> E. x e. A ps ) ) $=
      ( wi wrex wral r19.35 cv wcel ax-1 r19.21ai imim1i sylbi ) ABFCDGACDHZBCD
      GZFAQFABCDIAPQAACDEACJDKLMNO $.
      $( [13-May-2012] $)
  $}

  $( Restricted specialization. $)
  ra42e $p |- ( ( x e. A /\ y e. B /\ ph ) -> E. x e. A E. y e. B ph ) $=
    ( cv wcel w3a wrex wa wex simp1 ra4e 3adant1 19.8a syl11anc df-rex sylibr
    ) BFDGZCFEGZAHZSACEIZJZBKZUBBDIUASUBUDSTALTAUBSACEMNUCBOPUBBDQR $.
    $( [4-Jun-2012] $)

  ${
    $d A x $.
    $( Two ways to express substitution of ` A ` for ` x ` in ` ph ` . $)
    alexeqd $p |- ( A e. B
      -> ( A. x ( x = A -> ph ) <-> E. x ( x = A /\ ph ) ) ) $=
      ( wcel cvv cv wceq wi wal wa wex wb elisset c0 cif eqeq2 imbi1d albidv
      anbi1d exbidv bibi12d 0ex elimel alexeq dedth syl ) CDECFEZBGZCHZAIZBJZUJ
      AKZBLZMZCDNUHUOUIUHCOPZHZAIZBJZUQAKZBLZMCOCUPHZULUSUNVAVBUKURBVBUJUQACUPU
      IQZRSVBUMUTBVBUJUQAVCTUAUBABUPCOFUCUDUEUFUG $.
      $( [4-Jun-2012] $)
  $}


  ${
    $d A x $.  $d A z $.  $d B w $.  $d B x y $.  $d B z y $.  $d ch w $.
    $d ch x $.  $d ph z $.  $d ps y $.
    cbvrex2v.1 $e |- ( x = z -> ( ph <-> ch ) ) $.
    cbvrex2v.2 $e |- ( y = w -> ( ch <-> ps ) ) $.
    $( Change bound variables of double restricted universal quantification,
       using implicit substitution. $)
    cbvrex2v $p |- ( E. x e. A E. y e. B ph <-> E. z e. A E. w e. B ps ) $=
      ( wrex weq rexbidv cbvrexv rexbii bitri ) AEILZDHLCEILZFHLBGILZFHLRSDFHDF
      MACEIJNOSTFHCBEGIKOPQ $.
      $( [2-Jul-2012] $)
  $}


  ${
    $d A x y $.  $d B y $.  $d C x $.  $d D x y $.  $d ch y $.  $d ph x y $.
    $d th x $.
    rcla42edv.1 $e |- ( x = A -> ( ps <-> th ) ) $.
    rcla42edv.2 $e |- ( y = B -> ( th <-> ch ) ) $.
    $( 2-variable restricted existential specialization, using implicit
       substitution.  ( ~ rcla42ev with an antecedent.) $)
    rcla42edv $p |- ( ( ph /\ A e. C /\ B e. D )
        -> ( ch -> E. x e. C E. y e. D ps ) ) $=
      ( wcel wrex wi wa w3a cv wceq anbi2d rcla42ev simpr reximi syl 3expia
      exp4b com3r 3imp ) AGIMZHJMZCBFJNZEINZOZUIUJAUMUIUJACULUIUJACPZULUIUJUNQA
      BPZFJNZEINULUOUNADPEFGHIJERGSBDAKTFRHSDCALTUAUPUKEIUOBFJABUBUCUCUDUEUFUGU
      H $.
      $( [2-Jul-2012] $)
  $}


