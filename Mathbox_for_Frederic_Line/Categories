$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Categories
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Cat $. $( The class of all categories. $)

  $( Extend class notation with the class of categories. $)
  ccat $a class Cat $.

  ${
    $d x d c j r f g h a $.
    $( A category is a deductive system where the composition is associative,
       and identities are neutral elements. $)
    df-cat $a |- Cat = { x | E. d E. c E. j E. r
    ( x = <. <. d , c >. , <. j , r >. >. /\
           ( ( <. <. d , c >. , <. j , r >. >. e. Ded /\
           A. f e. dom d A. g e. dom d A. h e. dom d
              (
                ( ( d ` h ) = ( c ` g ) /\ ( d ` g ) = ( c ` f ) )
              ->
                ( h r ( g r f ) ) = ( ( h r g ) r f )
               ) ) /\
          ( A. a e. dom j A. f e. dom d ( ( c ` f ) = a ->
             ( ( j ` a ) r f ) = f ) /\
          A. a e. dom j A. f e. dom d ( ( d ` f ) = a ->
             ( f r ( j ` a ) ) = f ) ) ) ) } $.
  $}

  ${
    $d C a c d f g h j r $.  $d D a c d f g h j r $.  $d J a c d f j r $.
    $d M c d j r f $.  $d O c d j r $.  $d R a c d f g h j r $.
    $d a c d f g h j r x $.
    iscat.1 $e |- M = dom D $.
    iscat.2 $e |- O = dom J $.
    $( The predicate "is a category". $)
    iscat $p |- ( ( ( D e. A /\ C e. B /\ J e. F ) /\ R e. G )
      -> ( <. <. D , C >. , <. J , R >. >. e. Cat
        <-> ( ( <. <. D , C >. , <. J , R >. >. e. Ded /\
           A. f e. M A. g e. M A. h e. M
              (
                ( ( D ` h ) = ( C ` g ) /\ ( D ` g ) = ( C ` f ) )
              ->
                ( h R ( g R f ) ) = ( ( h R g ) R f )
               ) ) /\
          ( A. a e. O A. f e. M ( ( C ` f ) = a ->
             ( ( J ` a ) R f ) = f ) /\
          A. a e. O A. f e. M ( ( D ` f ) = a ->
             ( f R ( J ` a ) ) = f ) ) ) ) ) $=
      ( vx vd vc vj vr wcel w3a wa cop cv wceq cded cfv co wi cdm wral wex cab
      ccat opeq1 opeq1d eleq1d dmeq syl6eqr eleq2d fveq1 eqeq1d anbi12d imbi1d
      imbi12d ralbidv2 raleqdv ralbidv opeq2 eqeq2d 2ralbidv anbi1d opeq2d
      opreq1d imbi2d opreq2d opreq eqtrd eqeq12d elo df-cat eleq2i syl5bb ) DAU
      BCBUBKIUBUCEJUBUDDCUEZKEUEZUEZQUFRUFZSUFZUEZTUFZUAUFZUEZUEZUGWOUHUBZHUFZW
      IUIZGUFZWJUIZUGZWSWIUIZFUFZWJUIZUGZUDZWQWSXCWMUJZWMUJZWQWSWMUJZXCWMUJZUGZ
      UKZHWIULZUMZGXMUMZFXMUMZUDZXDNUFZUGZXRWLUIZXCWMUJZXCUGZUKZFXMUMZNWLULZUMZ
      XCWIUIZXRUGZXCXTWMUJZXCUGZUKZFXMUMZNYEUMZUDZUDZUDUAUNTUNSUNRUNQUOZUBWHUHU
      BZWQDUIZWSCUIZUGZWSDUIZXCCUIZUGZUDZWQWSXCEUJZEUJZWQWSEUJZXCEUJZUGZUKZHLUM
      ZGLUMFLUMZUDZUUBXRUGZXRKUIZXCEUJZXCUGZUKZFLUMNMUMZXCDUIZXRUGZXCUUOEUJZXCU
      GZUKZFLUMNMUMZUDZUDZWHUPUBYODWJUEZWNUEZUHUBZYRWTUGZUUAXDUGZUDZXKUKZHLUMZG
      LUMZFLUMZUDZYCFLUMZNYEUMZUVAYJUKZFLUMZNYEUMZUDZUDWFWNUEZUHUBZUUDXKUKZHLUM
      ZGLUMFLUMZUDZUUNYBUKZFLUMZNYEUMZUWCUDZUDWFKWMUEZUEZUHUBZUWIUDZUUNUUOXCWMU
      JZXCUGZUKZFLUMZNMUMZUVAXCUUOWMUJZXCUGZUKZFLUMZNMUMZUDZUDUVGQRSUATDCKEABIJ
      WIDUGZXQUVRYNUWDUXJWPUVJXPUVQUXJWOUVIUHUXJWKUVHWNWIDWJUQURUSUXJXOUVPFXMLU
      XJXCXMUBZXCLUBZXOUVPUXJXMLXCUXJXMDULLWIDUTOVAZVBZUXJXNUVOGXMLUXJWSXMUBWSL
      UBXNUVOUXJXMLWSUXMVBUXJXLUVNHXMLUXJWQXMUBWQLUBXLUVNUXJXMLWQUXMVBUXJXFUVMX
      KUXJXAUVKXEUVLUXJWRYRWTWQWIDVCVDUXJXBUUAXDWSWIDVCVDVEVFVGVHVGVHVGVHVEUXJY
      FUVTYMUWCUXJYDUVSNYEUXJYCFXMLUXMVIVJUXJYLUWBNYEUXJYKUWAFXMLUXJUXKUXLYKUWA
      UXNUXJYHUVAYJUXJYGUUTXRXCWIDVCVDVFVGVHVJVEVEWJCUGZUVRUWJUWDUWNUXOUVJUWFUV
      QUWIUXOUVIUWEUHUXOUVHWFWNWJCDVKURUSUXOUVOUWHFGLLUXOUVNUWGHLUXOUVMUUDXKUXO
      UVKYTUVLUUCUXOWTYSYRWSWJCVCVLUXOXDUUBUUAXCWJCVCZVLVEVFVJVMVEUXOUVTUWMUWCU
      XOYCUWKNFYELUXOXSUUNYBUXOXDUUBXRUXPVDVFVMVNVEWLKUGZUWJUWRUWNUXIUXQUWFUWQU
      WIUXQUWEUWPUHUXQWNUWOWFWLKWMUQVOUSVNUXQUWMUXCUWCUXHUXQUWLUXBNYEMUXQXRYEUB
      ZXRMUBZUWLUXBUXQYEMXRUXQYEKULMWLKUTPVAVBZUXQUWKUXAFLUXQYBUWTUUNUXQYAUWSXC
      UXQXTUUOXCWMXRWLKVCZVPVDVQVJVGVHUXQUWBUXGNYEMUXQUXRUXSUWBUXGUXTUXQUWAUXFF
      LUXQYJUXEUVAUXQYIUXDXCUXQXTUUOXCWMUYAVRVDVQVJVGVHVEVEWMEUGZUWRUUMUXIUVFUY
      BUWQYQUWIUULUYBUWPWHUHUYBUWOWGWFWMEKVKVOUSUYBUWHUUKFGLLUYBUWGUUJHLUYBXKUU
      IUUDUYBXHUUFXJUUHUYBXHWQUUEWMUJUUFUYBXGUUEWQWMWSXCWMEVSVRWQUUEWMEVSVTUYBX
      JXIXCEUJUUHXIXCWMEVSUYBXIUUGXCEWQWSWMEVSVPVTWAVQVJVMVEUYBUXCUUSUXHUVEUYBU
      XAUURNFMLUYBUWTUUQUUNUYBUWSUUPXCUUOXCWMEVSVDVQVMUYBUXFUVDNFMLUYBUXEUVCUVA
      UYBUXDUVBXCXCUUOWMEVSVDVQVMVEVEWBUPYPWHQFGHTUANSRWCWDWE $.
      $( [24-Oct-2007] $)
  $}

  ${
    $d C a c d f g h j r $.  $d D a c d f g h j r $.  $d J a c d f j r $.
    $d M c d j r f $.  $d O c d j r $.  $d R a c d f g h j r $.
    $d T c d j r x $.  $d a c d f g h j r x $.
    cati.1 $e |- D = ( dom_ ` T ) $.
    cati.2 $e |- C = ( cod_ ` T ) $.
    cati.3 $e |- J = ( id_ ` T ) $.
    cati.4 $e |- R = ( o_ ` T ) $.
    cati.5 $e |- M = dom D $.
    cati.6 $e |- O = dom J $.
    $( Definitional properties of a category. $)
    cati $p |- ( T e. Cat
       -> ( ( <. <. D , C >. , <. J , R >. >. e. Ded /\
           A. f e. M A. g e. M A. h e. M
              (
                ( ( D ` h ) = ( C ` g ) /\ ( D ` g ) = ( C ` f ) )
              ->
                ( h R ( g R f ) ) = ( ( h R g ) R f )
               ) ) /\
          ( A. a e. O A. f e. M ( ( C ` f ) = a ->
             ( ( J ` a ) R f ) = f ) /\
          A. a e. O A. f e. M ( ( D ` f ) = a ->
             ( f R ( J ` a ) ) = f ) ) ) ) $=
      ( vx vd vc vj vr ccat wcel cv cop wceq cded cfv wa co wi cdm wral wex
      cab df-cat eleq2i c1st wb domval eqcomi eqeq2i opeq1 opeq1d eleq1d dmeq
      syl6eqr eleq2d fveq1 eqeq1d anbi12d imbi1d imbi12d ralbidv2 raleqdv
      ralbidv sylbi c2nd codval opeq2 eqeq2d 2ralbidv anbi1d idval opeq2d
      opreq1d imbi2d opreq2d cmpval opreq eqtrd eqeq12d eloi ) DUCUDDRUESUEZTUE
      ZUFZUAUEZUBUEZUFZUFZUGXAUHUDZGUEZWOUIZFUEZWPUIZUGZXEWOUIZEUEZWPUIZUGZUJZX
      CXEXIWSUKZWSUKZXCXEWSUKZXIWSUKZUGZULZGWOUMZUNZFXSUNZEXSUNZUJZXJKUEZUGZYDW
      RUIZXIWSUKZXIUGZULZEXSUNZKWRUMZUNZXIWOUIZYDUGZXIYFWSUKZXIUGZULZEXSUNZKYKU
      NZUJZUJZUJUBUOUAUOTUOSUORUPZUDBAUFZHCUFZUFZUHUDZXCBUIZXEAUIZUGZXEBUIZXIAU
      IZUGZUJZXCXEXICUKZCUKZXCXECUKZXICUKZUGZULZGIUNZFIUNEIUNZUJZUUKYDUGZYDHUIZ
      XICUKZXIUGZULZEIUNKJUNZXIBUIZYDUGZXIUVDCUKZXIUGZULZEIUNKJUNZUJZUJZUCUUBDR
      EFGUAUBKTSUQURUUABWPUFZWTUFZUHUDZUUGXFUGZUUJXJUGZUJZXQULZGIUNZFIUNZEIUNZU
      JZYIEIUNZKYKUNZUVJYPULZEIUNZKYKUNZUJZUJZUUCWTUFZUHUDZUUMXQULZGIUNZFIUNEIU
      NZUJZUVCYHULZEIUNZKYKUNZUWLUJZUJZUUCHWSUFZUFZUHUDZUWSUJZUVCUVDXIWSUKZXIUG
      ZULZEIUNZKJUNZUVJXIUVDWSUKZXIUGZULZEIUNZKJUNZUJZUJZUVPRSTUBUADWODUSUIZUSU
      IZUGWOBUGZUUAUWNUTUYCBWOBUYCBDLVAVBVCUYDYCUWGYTUWMUYDXBUVSYBUWFUYDXAUVRUH
      UYDWQUVQWTWOBWPVDVEVFUYDYAUWEEXSIUYDXIXSUDZXIIUDZYAUWEUYDXSIXIUYDXSBUMIWO
      BVGPVHZVIZUYDXTUWDFXSIUYDXEXSUDXEIUDXTUWDUYDXSIXEUYGVIUYDXRUWCGXSIUYDXCXS
      UDXCIUDXRUWCUYDXSIXCUYGVIUYDXLUWBXQUYDXGUVTXKUWAUYDXDUUGXFXCWOBVJVKUYDXHU
      UJXJXEWOBVJVKVLVMVNVOVNVOVNVOVLUYDYLUWIYSUWLUYDYJUWHKYKUYDYIEXSIUYGVPVQUY
      DYRUWKKYKUYDYQUWJEXSIUYDUYEUYFYQUWJUYHUYDYNUVJYPUYDYMUVIYDXIWOBVJVKVMVNVO
      VQVLVLVRWPUYBVSUIZUGWPAUGZUWNUXEUTUYIAWPAUYIADMVTVBVCUYJUWGUWTUWMUXDUYJUV
      SUWPUWFUWSUYJUVRUWOUHUYJUVQUUCWTWPABWAVEVFUYJUWDUWREFIIUYJUWCUWQGIUYJUWBU
      UMXQUYJUVTUUIUWAUULUYJXFUUHUUGXEWPAVJWBUYJXJUUKUUJXIWPAVJZWBVLVMVQWCVLUYJ
      UWIUXCUWLUYJYIUXAKEYKIUYJYEUVCYHUYJXJUUKYDUYKVKVMWCWDVLVRWRDVSUIZUSUIZUGW
      RHUGZUXEUYAUTUYMHWRHUYMDHNWEVBVCUYNUWTUXIUXDUXTUYNUWPUXHUWSUYNUWOUXGUHUYN
      WTUXFUUCWRHWSVDWFVFWDUYNUXCUXNUWLUXSUYNUXBUXMKYKJUYNYDYKUDZYDJUDZUXBUXMUY
      NYKJYDUYNYKHUMJWRHVGQVHVIZUYNUXAUXLEIUYNYHUXKUVCUYNYGUXJXIUYNYFUVDXIWSYDW
      RHVJZWGVKWHVQVNVOUYNUWKUXRKYKJUYNUYOUYPUWKUXRUYQUYNUWJUXQEIUYNYPUXPUVJUYN
      YOUXOXIUYNYFUVDXIWSUYRWIVKWHVQVNVOVLVLVRWSUYLVSUIZUGWSCUGZUYAUVPUTUYSCWSC
      UYSDCOWJVBVCUYTUXIUVBUXTUVOUYTUXHUUFUWSUVAUYTUXGUUEUHUYTUXFUUDUUCWSCHWAWF
      VFUYTUWRUUTEFIIUYTUWQUUSGIUYTXQUURUUMUYTXNUUOXPUUQUYTXNXCUUNWSUKUUOUYTXMU
      UNXCWSXEXIWSCWKWIXCUUNWSCWKWLUYTXPUUPXIWSUKUUQUYTXOUUPXIWSXCXEWSCWKWGUUPX
      IWSCWKWLWMWHVQWCVLUYTUXNUVHUXSUVNUYTUXLUVGKEJIUYTUXKUVFUVCUYTUXJUVEXIUVDX
      IWSCWKVKWHWCUYTUXQUVMKEJIUYTUXPUVLUVJUYTUXOUVKXIXIUVDWSCWKVKWHWCVLVLVRWNV
      R $.
      $( [24-Oct-2007] $)
  $}

  $( Lemma for ~ 0ded . $)
  0alg $p |- <. <. (/) , (/) >. , <. (/) , (/) >. >. e. Alg $=
    ( c0 cop calg wcel wf w3a wfun cdm cxp wss crn cvv wa wb 0ex 3pm3.2i dm0
    eqcomi isalg mp2an f0 fun0 ssid xp0r 3sstr4i rn0 eqimssi mpbir2an ) AABZUIB
    CDZAAAEZUKUKFZAGZAHZAAIZJZAKZAJZFZALDZUTUTFUTUJULUSMNUTUTUTOOOPOLLAAALLAAAU
    NAQRZVASTUKUKUKAUAZVBVBPUMUPURUBAAUNUOAUCQAUDUEUQAUFUGPUH $.
    $( [10-Jan-2008] $)


  ${
    $d a f g $.
    $( A deductive system with no object and no morphism. $)
    0ded $p |- <. <. (/) , (/) >. , <. (/) , (/) >. >. e. Ded $=
      ( va vg vf c0 cop cded wcel calg cv cfv wceq wa cdm wral wb w3a co wi
      cvv 0ex 3pm3.2i eqid isded mp2an 0alg dm0 eleq2i noel pm2.21i sylbi rgen
      pm3.2i mpbir2an ) DDEZUNEZFGZUOHGZAIZDJDJURKZUSLZADMZNZBIZCIZEVAGVCDJZVDD
      JZKZOBVANZCVANZPZVGVCVDDQDJZVFKRBVANZCVANZVGVKVEKRBVANZCVANZLZDSGZVQVQPVQ
      UPVJVPLOVQVQVQTTTUATSSDDDCBSSDVAVAAVAUBZVRUCUDUQVBVIUEUTAVAURVAGURDGZUTVA
      DURUFUGVSUTURUHUIUJUKVHCVAVDVAGZVDDGZVHVADVDUFUGZWAVHVDUHZUIUJUKUAVMVOVLC
      VAVTWAVLWBWAVLWCUIUJUKVNCVAVTWAVNWBWAVNWCUIUJUKULUM $.
      $( [10-Jan-2008] $)
  $}

  ${
    $d a f g h $.
    $( Category ` 0 ` has no object and no morphism. $)
    0cat $p |- <. <. (/) , (/) >. , <. (/) , (/) >. >. e. Cat $=
      ( vh vg vf va c0 cop ccat wcel cded cv cfv wceq wa co wi cdm wral cvv
      w3a wb 0ex 3pm3.2i eqid iscat mp2an 0ded dm0 eleq2i noel pm2.21i sylbi
      rgen pm3.2i mpbir2an ) EEFZUOFZGHZUPIHZAJZEKBJZEKZLVACJZEKZLMUSUTVBENENUS
      UTENVBENLOAEPZQBVDQZCVDQZMZVCDJZLZVHEKZVBENVBLOCVDQZDVDQZVIVBVJENVBLOCVDQ
      ZDVDQZMZERHZVPVPSVPUQVGVOMTVPVPVPUAUAUAUBUARREEECBARREVDVDDVDUCZVQUDUEURV
      FUFVECVDVBVDHVBEHZVEVDEVBUGUHVRVEVBUIUJUKULUMVLVNVKDVDVHVDHZVHEHZVKVDEVHU
      GUHZVTVKVHUIZUJUKULVMDVDVSVTVMWAVTVMWBUJUKULUMUN $.
      $( [10-Jan-2008] $)
  $}


  ${
    $d A u x y z $.
    1cat.1 $e |- A e. _V $.
    $( Category ` 1 ` has one object and one morphism. $)
    1cat $p |- <. <. { <. <. A , A >. , A >. } ,
     { <. <. A , A >. , A >. } >. , <. { <. A , <. A , A >. >. } ,
     { <. <. <. A , A >. , <. A , A >. >. , <. A , A >. >. } >. >.
     e. Cat $=
      ( vu vz vy vx cop csn ccat wcel cded cv cfv wceq wa co wi wral cvv w3a
      wb snex 3pm3.2i cdm dmsnop eqcomi iscat mp2an 1ded id eqcomd 3ad2ant3
      3ad2ant2 3ad2ant1 opreq12d opreq1d opreq2d 3eqtrd eqeq12d df-opr opex
      fvsn eqtr2i eqtri opreq12i a1i syl5bi elsni syl3an rgen3 pm3.2i adantr
      fveq2d adantl opreq1i 3eqtri syl2an rgen2 opreq2i mpbir2an ) AAGZAGZHZWCG
      AWAGZHZWAWAGZWAGZHZGGZIJZWIKJZCLZWCMDLZWCMZNWNELZWCMZNOZWLWMWOWHPZWHPZWLW
      MWHPZWOWHPZNZQZCWAHZRDXDREXDRZOZWPFLZNZXGWEMZWOWHPZWONZQZEXDRFAHZRZXHWOXI
      WHPZWONZQZEXDRFXMRZOZWCSJZXTWESJZTWHSJWJXFXSOUAXTXTYAWBUBZYBWDUBUCWGUBSSW
      CWCWHEDCSSWEXDXMFWCUDXDWAAUEUFWEUDXMAWAUEUFUGUHWKXEABUIXCEDCXDXDXDWOWANZW
      MWANZWLWANZXCWOXDJZWMXDJWLXDJYCYDYETZWAWAWAWHPZWHPZYHWAWHPZNZXBWQYGYIWSYJ
      XAYGWAWLYHWRWHYEYCWAWLNYDYEWLWAYEUJUKZULYGWAWMWAWOWHYDYCWAWMNYEYDWMWAYDUJ
      UKZUMYCYDWAWONZYEYCWOWAYCUJUKZUNUOUOYGYJWLWAWHPZWAWHPZWTWAWHPZXAYEYCYJYQN
      YDYEYHYPWAWHYEWAWLWAWHYLUPUPULYDYCYQYRNYEYDYPWTWAWHYDWAWMWLWHYMUQUPUMYCYD
      YRXANYEYCWAWOWTWHYOUQUNURUSYKWQWAYHYHWAWHYHWFWHMZWAWAWAWHUTZWFWAWAWAVAAAV
      AZVBZVCYHYSWAYTUUBVDVEVFVGWOWAVHZWMWAVHWLWAVHVIVJVKXNXRXLFEXMXDXGANZYCXLX
      GXMJZYFUUDYCOZAWEMZWAWHPZWANZXKXHUUFUUHXJWAWOUUFUUGXIWAWOWHUUFAXGWEUUDAXG
      NYCUUDXGAUUDUJUKVLVMZYCYNUUDYOVNZUOUUKUSUUIXHUUHYHYSWAUUGWAWAWHAWABUUAVBZ
      VOYTUUBVPVFVGXGAVHZUUCVQVRXQFEXMXDUUDYCXQUUEYFUUFWAUUGWHPZWANZXPXHUUFUUNX
      OWAWOUUFWAWOUUGXIWHUUKUUJUOUUKUSUUOXHUUNYHYSWAUUGWAWAWHUULVSYTUUBVPVFVGUU
      MUUCVQVRVKVT $.
      $( [30-Oct-2007] $)
  $}

  ${
    $d f g t u v w x y z $.
    $( Structure of a category. $)
    strcat $p |- Cat C_ ( ( _V X. _V ) X. ( _V X. _V ) ) $=
      ( vx vg vf vv vu vw vz vy vt ccat cv cop wceq cded wcel cfv wa co wi cdm
      wral wex cab cvv cxp df-cat stcat eqsstri ) JAKBKZCKZLDKZEKZLLZMUMNOFKZUI
      PGKZUJPMUOUIPHKZUJPZMQUNUOUPULRULRUNUOULRUPULRMSFUITZUAGURUAHURUAQUQIKZMU
      SUKPZUPULRUPMSHURUAIUKTZUAUPUIPUSMUPUTULRUPMSHURUAIVAUAQQZQEUBDUBCUBBUBAU
      CUDUDUEZVCUEAHGFDEICBUFVBABCEDUGUH $.
      $( [26-Oct-2007] $)
  $}

  $( A category is a relation. $)
  relcat $p |- Rel Cat $=
    ( ccat wrel cvv cxp wss strcat xpss sstri df-rel mpbir ) ABACCDZEAKKDKFKKGH
    AIJ $.
    $( [26-Oct-2007] $)

  $( The domain of a category is a relation. $)
  reldcat $p |- Rel dom Cat $=
    ( ccat cdm wrel cvv cxp wss strcat dmss ax-mp dmxpid sseqtri df-rel mpbir
    ) ABZCNDDEZFNOOEZBZOAPFNQFGAPHIOJKNLM $.
    $( [26-Oct-2007] $)

  $( The range of a category is a relation. $)
  relrcat $p |- Rel ran Cat $=
    ( ccat crn wrel cvv cxp wss strcat rnss ax-mp c0 wne wceq wa vn0 pm3.2i
    xpnz mpbi rnxp sseqtri df-rel mpbir ) ABZCUBDDEZFUBUCUCEZBZUCAUDFUBUEFGAUDH
    IUCJKZUEUCLDJKZUGMUFUGUGNNODDPQUCUCRISUBTUA $.
    $( [26-Oct-2007] $)

  ${
    $d T w x y z $.
    $( A category is a deductive system. $)
    catded $p |- ( T e. Cat -> T e. Ded ) $=
      ( vz vy vx vw ccat wcel cdom_ cfv ccod_ cop cid_ co_ cded c1st c2nd wrel
      cdm crn w3a wceq relcat reldcat relrcat 3pm3.2i 11st22nd mpan eqid
      domval codval opeq12i idval cmpval syl6eqr cv wa co wi wral cati pm3.26d
      eqeltrd ) AFGZAAHIZAJIZKZALIZAMIZKZKZNVCAAOIZOIZVKPIZKZAPIZOIZVOPIZKZKZVJ
      FQZFRQZFSQZTVCAVSUAVTWAWBUBUCUDUEAFUFUGVFVNVIVRVDVLVEVMVDAVDUHZUIVEAVEUHZ
      UJUKVGVPVHVQAVGVGUHZULAVHVHUHZUMUKUKUNVCVJNGZBUOZVDICUOZVEIUAWIVDIDUOZVEI
      ZUAUPWHWIWJVHUQVHUQWHWIVHUQWJVHUQUAURBVDRZUSCWLUSDWLUSZVCWGWMUPWKEUOZUAWN
      VGIZWJVHUQWJUAURDWLUSEVGRZUSWJVDIWNUAWJWOVHUQWJUAURDWLUSEWPUSUPVEVDVHADCB
      VGWLWPEWCWDWEWFWLUHWPUHUTVAVAVB $.
      $( [26-Oct-2007] $)
  $}

  ${
    cat.1 $e |- M = dom D $.
    cat.3 $e |- D = ( dom_ ` T ) $.
    ${
      cat.2 $e |- O = dom J $.
      cat.5 $e |- J = ( id_ ` T ) $.
      ${
        $( The 1st "axiom" of a category: ` ( dom_ `` T ) ` is a mapping from
           the morphisms of ` T ` to the objects of ` T ` . $)
        domc $p |- ( T e. Cat -> D : M --> O ) $=
          ( ccat wcel cded calg wf catded dedalg doma 3syl ) BJKBLKBMKDEANBOBPA
          BCDEFGHIQR $.
          $( [2-Jan-2008] $)
      $}

      ${
        cat.4 $e |- C = ( cod_ ` T ) $.
        $( The 2nd "axiom" of a category ` ( cod_ `` T ) ` is a mapping from
           the morphisms of ` T ` to the objects of ` T ` . $)
        codc $p |- ( T e. Cat -> C : M --> O ) $=
          ( ccat wcel cded calg wf catded dedalg coda 3syl ) CLMCNMCOMEFAPCQCRA
          BCDEFGHIJKST $.
          $( [2-Jan-2008] $)
      $}

      ${
        $( The 3rd "axiom" of a category ` ( id_ `` T ) ` is a mapping from the
           objects of ` T ` to the morphisms of ` T ` . $)
        idc $p |- ( T e. Cat -> J : O --> M ) $=
          ( ccat wcel cded calg wf catded dedalg ida 3syl ) BJKBLKBMKEDCNBOBPAB
          CDEFGHIQR $.
          $( [5-Dec-2007] $)
      $}
    $}

    ${
      cat.6 $e |- R = ( o_ ` T ) $.
      $( The 4th "axiom" of a category: ` ( o_ `` T ) ` is a partial operation
         from the morphisms of ` T ` to the morphisms of ` T ` . $)
      cmppfc $p |- ( T e. Cat ->
        ( Fun R /\ dom R C_ ( M X. M ) /\ ran R C_ M ) ) $=
        ( ccat wcel cded calg wfun cdm cxp wss crn w3a catded dedalg cmppfa
        3syl ) CHICJICKIBLBMDDNOBPDOQCRCSABCDEFGTUA $.
        $( [10-Mar-2008] $)
    $}
  $}

  ${
    idosc.1 $e |- O = dom J $.
    idosc.2 $e |- D = ( dom_ ` T ) $.
    idosc.3 $e |- J = ( id_ ` T ) $.
    idosc.4 $e |- C = ( cod_ ` T ) $.
    $( The 5th "axiom" of a category: identities are morphisms whose domains
       and codomains are equal. $)
    idosc $p |- ( ( T e. Cat /\ A e. O ) -> ( ( D ` ( J ` A ) ) = A /\
        ( C ` ( J ` A ) ) = A ) ) $=
      ( cded wcel cfv wceq wa ccat idosd catded sylan ) DKLAFLAEMZCMANTBMANODPL
      ABCDEFGHIJQDRS $.
      $( [5-Dec-2007] $)
  $}

  ${
    cmppfcd.1 $e |- M = dom D $.
    cmppfcd.2 $e |- D = ( dom_ ` T ) $.
    cmppfcd.3 $e |- C = ( cod_ ` T ) $.
    cmppfcd.4 $e |- R = ( o_ ` T ) $.
    $( The 6th "axiom" of a category: ` ( G ( o_ `` T ) F ) ` is only defined
       when the domain of ` F ` equals the codomain of ` G ` . $)
    cmppfcd $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
           ( <. G , F >. e. dom R <-> ( D ` G ) = ( C ` F ) ) ) $=
      ( cded wcel cop cdm cfv wceq wb ccat cmppfd catded syl3an1 ) DLMEGMFGMFEN
      COMFBPEAPQRDSMABCDEFGHIJKTDUAUB $.
      $( [10-Mar-2008] $)


    $( The 7th "axiom" of a category: when ` ( G ( o_ `` T ) F ) ` is defined
       its domain is the domain of ` F ` . $)
    domcmpc $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
        ( ( D ` G ) = ( C ` F ) -> ( D ` ( G R F ) ) = ( D ` F ) ) ) $=
      ( cded wcel cfv wceq co wi ccat domcmpd catded syl3an1 ) DLMEGMFGMFBNEANO
      FECPBNEBNOQDRMABCDEFGHIJKSDTUA $.
      $( [10-Mar-2008] $)


    $( The 8th "axiom" of a category: when ` ( G ( o_ `` T ) F ) ` is defined
       its codomain is the codomain of ` G ` . $)
    codcmpc $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
              ( ( D ` G ) = ( C ` F )
              -> ( C ` ( G R F ) ) = ( C ` G ) ) ) $=
      ( cded wcel cfv wceq co wi ccat codcmpd catded syl3an1 ) DLMEGMFGMFBNEANO
      FECPANFANOQDRMABCDEFGHIJKSDTUA $.
      $( [10-Jan-2008] $)
  $}


  ${
    $d C f g h x $.  $d D f g h x $.  $d F f g h $.  $d G g h $.  $d H h $.
    $d M f g h $.  $d R f g h x $.  $d T f x $.
    cmpasso.1 $e |- M = dom D $.
    cmpasso.2 $e |- D = ( dom_ ` T ) $.
    cmpasso.5 $e |- C = ( cod_ ` T ) $.
    cmpasso.6 $e |- R = ( o_ ` T ) $.
    $( The 9th "axiom" of a category: ` ( o_ `` T ) ` is associative. $)
    cmpasso $p |- ( ( T e. Cat /\ ( F e. M /\ G e. M /\ H e. M ) ) ->
              (
                ( ( D ` H ) = ( C ` G ) /\
                ( D ` G ) = ( C ` F ) )
              ->
                ( H R ( G R F ) ) =
                  ( ( H R G ) R F )
               ) ) $=
      ( vh vg vf vx ccat wcel w3a cfv wceq wa co wi cop cid_ cded cv wral cdm
      eqid cati fveq2 eqeq2d anbi2d opreq2 opreq2d eqeq12d imbi12d eqeq1d
      anbi12d opreq1 opreq1d anbi1d rcla43v com12 ad2antlr syl imp ) DQRZEHRFHR
      GHRSZGBTZFATZUAZFBTZEATZUAZUBZGFECUCZCUCZGFCUCZECUCZUAZUDZVJBAUEDUFTZCUEU
      EUGRZMUHZBTZNUHZATZUAZWIBTZOUHZATZUAZUBZWGWIWMCUCZCUCZWGWICUCZWMCUCZUAZUD
      ZMHUINHUIOHUIZUBWNPUHZUAXDWETZWMCUCWMUAUDOHUIPWEUJZUIWMBTXDUAWMXECUCWMUAU
      DOHUIPXFUIUBZUBVKWDUDZABCDONMWEHXFPJKWEUKLIXFUKULXCXHWFXGVKXCWDXBWDWKWLVP
      UAZUBZWGWIECUCZCUCZWSECUCZUAZUDWHVMUAZVQUBZWGVSCUCZWGFCUCZECUCZUAZUDONMEF
      GHHHWMEUAZWPXJXAXNYAWOXIWKYAWNVPWLWMEAUMUNUOYAWRXLWTXMYAWQXKWGCWMEWICUPUQ
      WMEWSCUPURUSWIFUAZXJXPXNXTYBWKXOXIVQYBWJVMWHWIFAUMUNYBWLVOVPWIFBUMUTVAYBX
      LXQXMXSYBXKVSWGCWIFECVBUQYBWSXRECWIFWGCUPVCURUSWGGUAZXPVRXTWCYCXOVNVQYCWH
      VLVMWGGBUMUTVDYCXQVTXSWBWGGVSCVBYCXRWAECWGGFCVBVCURUSVEVFVGVHVI $.
      $( [29-Oct-2007] $)
  $}

  ${
    $d A a f $.  $d C a f x y $.  $d D a f x y $.  $d F f $.  $d J a f $.
    $d M a f $.  $d O a $.  $d R a f x y $.
    cmpida.1 $e |- M = dom D $.
    cmpida.2 $e |- D = ( dom_ ` T ) $.
    cmpida.3 $e |- O = dom J $.
    cmpida.4 $e |- J = ( id_ ` T ) $.
    cmpida.5 $e |- R = ( o_ ` T ) $.
    cmpida.6 $e |- C = ( cod_ ` T ) $.
    $( The 10th "axiom" of a category: ` ( J `` A ) ` is a left neutral
       element. $)
    cmpida $p |- ( ( T e. Cat /\ A e. O /\ F e. M ) -> ( ( C ` F ) = A
         -> ( ( J ` A ) R F ) = F ) ) $=
      ( vy vx vf va ccat wcel cfv wceq co wi cop cded cv wa wral cati eqeq2
      fveq2 opreq1d eqeq1d imbi12d opreq2 id eqeq12d rcla42v com12 ad2antrl
      syl 3impib ) ETUAZAIUAZFHUAZFBUBZAUCZAGUBZFDUDZFUCZUEZVECBUFGDUFUFUGUAPUH
      ZCUBQUHZBUBUCVOCUBRUHZBUBZUCUIVNVOVPDUDDUDVNVODUDVPDUDUCUEPHUJQHUJRHUJUIZ
      VQSUHZUCZVSGUBZVPDUDZVPUCZUEZRHUJSIUJZVPCUBVSUCVPWADUDVPUCUERHUJSIUJZUIUI
      VFVGUIZVMUEZBCDERQPGHISKOMNJLUKWEWHVRWFWGWEVMWDVMVQAUCZVJVPDUDZVPUCZUESRA
      FIHVSAUCZVTWIWCWKVSAVQULWLWBWJVPWLWAVJVPDVSAGUMUNUOUPVPFUCZWIVIWKVLWMVQVH
      AVPFBUMUOWMWJVKVPFVPFVJDUQWMURUSUPUTVAVBVCVD $.
      $( [29-Oct-2007] $)
  $}

  ${
    $d A a f $.  $d D a f x y $.  $d F f $.  $d J a f $.  $d M a f $.
    $d O a $.  $d R a f x y $.  $d T a f x y $.
    cmpidb.1 $e |- M = dom D $.
    cmpidb.2 $e |- D = ( dom_ ` T ) $.
    cmpidb.3 $e |- O = dom J $.
    cmpidb.4 $e |- J = ( id_ ` T ) $.
    cmpidb.5 $e |- R = ( o_ ` T ) $.
    $( The 11th "axiom" of a category: ` ( J `` A ) ` is a right neutral
       element. $)
    cmpidb $p |- ( ( T e. Cat /\ A e. O /\ F e. M ) -> ( ( D ` F ) = A
         -> ( F R ( J ` A ) ) = F ) ) $=
      ( vy vx vf va ccat wcel cfv wceq co wi ccod_ cop cded cv wa wral eqid
      cati eqeq2 fveq2 opreq2d eqeq1d imbi12d opreq1 id eqeq12d rcla42v com12
      ad2antll syl 3impib ) DRSZAHSZEGSZEBTZAUAZEAFTZCUBZEUAZUCZVEBDUDTZUEFCUEU
      EUFSNUGZBTOUGZVNTUAVPBTPUGZVNTZUAUHVOVPVQCUBCUBVOVPCUBVQCUBUAUCNGUIOGUIPG
      UIUHZVRQUGZUAVTFTZVQCUBVQUAUCPGUIQHUIZVQBTZVTUAZVQWACUBZVQUAZUCZPGUIQHUIZ
      UHUHVFVGUHZVMUCZVNBCDPONFGHQJVNUJLMIKUKWHWJVSWBWIWHVMWGVMWCAUAZVQVJCUBZVQ
      UAZUCQPAEHGVTAUAZWDWKWFWMVTAWCULWNWEWLVQWNWAVJVQCVTAFUMUNUOUPVQEUAZWKVIWM
      VLWOWCVHAVQEBUMUOWOWLVKVQEVQEVJCUQWOURUSUPUTVAVBVCVD $.
      $( [24-Oct-2007] $)
  $}

  ${
    dmo.1 $e |- M = dom ( dom_ ` T ) $.
    dmo.3 $e |- O = dom ( id_ ` T ) $.

    ${
      dmo.2 $e |- D = ( dom_ ` T ) $.
      $( The domain of a morphism is an object. $)
      dmo $p |- ( ( T e. Cat /\ F e. M ) -> ( D ` F ) e. O ) $=
        ( wf wcel cfv ccat ffvelrn cid_ cdom_ cdm eqcomi dmeqi eqtri eqid domc
        sylan ) DEAICDJCAKEJBLJDECAMABBNKZDEDBOKZPAPFUDAAUDHQRSHGUCTUAUB $.
        $( [2-Jan-2008] $)

    $}

    ${
      cdmo.2 $e |- C = ( cod_ ` T ) $.
      $( The codomain of a morphism is an object. $)
      cdmo $p |- ( ( T e. Cat /\ F e. M ) -> ( C ` F ) e. O ) $=
        ( wf wcel cfv ccat ffvelrn cdom_ cid_ eqid codc sylan ) DEAICDJCAKEJBLJ
        DECAMABNKZBBOKZDEFSPGTPHQR $.
        $( [2-Jan-2008] $)
    $}

    ${
      jdmo.2 $e |- J = ( id_ ` T ) $.
      $( An identity is a morphism. $)
      jdmo $p |- ( ( T e. Cat /\ A e. O ) -> ( J ` A ) e. M ) $=
        ( wf wcel cfv ccat ffvelrn cdom_ eqid cid_ cdm dmeqi eqtr4i idc sylan
        ) EDCIAEJACKDJBLJEDACMBNKZBCDEFUBOEBPKZQCQGCUCHRSHTUA $.
        $( [10-Jan-2008] $)
    $}

  $}

  ${
    cmpmorp.1 $e |- M = dom ( dom_ ` T ) $.
    cmpmorp.2 $e |- D = ( dom_ ` T ) $.
    cmpmorp.3 $e |- C = ( cod_ ` T ) $.
    cmpmorp.4 $e |- R = ( o_ ` T ) $.
    $( Conditions for a composite to be a morphism. $)
    cmpmorp $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
      ( ( D ` G ) = ( C ` F ) -> ( G R F ) e. M ) ) $=
      ( ccat wcel w3a cfv wceq co crn wfun cop cdm wa cxp wss cdom_ eqid
      cmppfc 3ad2ant1 3simp1d adantr eqcomi dmeqi eqtri cmppfcd biimpar jca ex
      fnoprvrn2 syl6 wi 3simp3 ssel 3syl syld ) DLMZEGMZFGMZNZFBOEAOPZFECQZCRZM
      ZVJGMZVHVICSZFETCUAZMZUBZVLVHVIVQVHVIUBVNVPVHVNVIVHVNVOGGUCUDZVKGUDZVEVFV
      NVRVSNZVGDUEOZCDGHWAUFKUGUHZUIUJVHVPVIABCDEFGGWAUABUAHWABBWAIUKULUMIJKUNU
      OUPUQFECURUSVHVTVSVLVMUTWBVNVRVSVAVKGVJVBVCVD $.
      $( [10-Mar-2008] $)
  $}

  $( Two ways to define the set of the morphisms of a category. $)
  morcat $p |- ( T e. Cat -> dom ( dom_ ` T ) = dom ( cod_ ` T ) ) $=
    ( ccat wcel cdom_ cfv cdm cid_ ccod_ wf wceq eqid codc fdm eqcomd syl ) ABC
    ADEZFZAGEZFZAHEZIZQTFZJTPARQSQKPKSKRKTKLUAUBQQSTMNO $.
    $( [19-Sep-2009] $)

  ${
    cmppfc1.1 $e |- R = ( o_ ` T ) $.
    $( Composition is a function. $)
    cmppfc1 $p |- ( T e. Cat -> Fun R ) $=
      ( ccat wcel wfun cdm cdom_ cfv cxp wss crn eqid cmppfc 3simp1d ) BDEAFAGB
      HIZGZQJKALQKPABQQMPMCNO $.
      $( [5-Oct-2009] $)

  $}

  ${
    $d D x y z $.  $d F x y z $.  $d G x y z $.  $d R x y z $.  $d T x y z $.
    dualcat2lem.1 $e |- D = ( dom_ ` T ) $.
    dualcat2lem.2 $e |- C = ( cod_ ` T ) $.
    dualcat2lem.3 $e |- R = ( o_ ` T ) $.
    $( Lemma for ~ dualcat2 . $)
    dualcat2lem $p |- (
       ( T e. Cat /\ ( F e. dom D /\ G e. dom D ) /\ ( D ` G ) = ( C ` F ) )
            -> ( F { <. <. x , y >. , z >.
            | ( <. y , x >. e. dom R /\ z = ( y R x ) ) } G )
            = ( G R F ) ) $=
      ( ccat wcel cdm wa cfv wceq cv cop co copab2 cxp wss wi wfun crn eqid
      cmppfc 3simp2d sseld anim1d ssoprab2g ccnv cnvxp eqcomi eleq2i visset
      opelcnv bitri anbi1i oprabbii twsvbdop eqtr sseq2 w3a oprssoprvg eqcomd
      wfn oprex fnoprab2 fnfun ax-mp a1i 3simpl3 cmppfcd biimprd 3expib a1dd
      3imp1 cvv wb elisset anim12i 3ad2ant2 adantr opelcnvg syl mpbird wrel
      relcnv dmoprabss6 bicomi dmeqi syl5eq eleq2d 3adant3 syl3ancOLD pm3.27
      opreq2 opreq1 oprabval2 ad2antlr eqtrd exp31 3imp mpd 3exp1 com3r syl6bi
      mp2an mpcom ) GMNZHEOZNZIXNNZPZIEQHDQRZHIBSZASZTZFOZNZCSXSXTFUAZRZPZABCUB
      ZUAZIHFUAZRZYGYAXNXNUCZNZYEPZABCUBZUDZXMXQXRYJUEZUEZXMYFYMABCXMYCYLYEXMYB
      YKYAXMFUFYBYKUDFUGXNUDEFGXNXNUHZJLUIUJUKULUMYNXTXSTZYKNZYEPZABCUBZRZUUBXT
      XNNXSXNNPYEPABCUBZRZYOXMYQUEZUEZYMUUAABCYLYTYEYLYAYKUNZNYTYKUUHYAUUHYKXNX
      NUOUPUQXSXTYKBURZAURZUSUTVAVBYEABCXNXNVCUUCUUEPYNUUDRZUUGYNUUBUUDVDUUKYOY
      GUUDUDZUUFYNUUDYGVEXMXQUULYPXMXQUULXRYJXMXQUULVFZXRPZYHHIUUDUAZRZYJUUDUFZ
      UULHITZYGOZNZUUPUUNUUQUULUUTVFUUOYHHIUUDYGVGVHUUQUUNUUDYKVIUUQABCXNXNYDUU
      DXSXTFVJZUUDUHZVKYKUUDVLVMVNXMXQUULXRVOUUNUUTUURYBUNZNZUUNUVDIHTYBNZXMXQU
      ULXRUVEXMXQXRUVEUEZUULXMXOXPUVFXMXOXPVFUVEXRDEFGHIXNYRJKLVPVQVRVSVTUUNHWA
      NZIWANZPZUVDUVEWBUUMUVIXRXQXMUVIUULXOUVGXPUVHHXNWCIXNWCWDWEWFHIWAWAYBWGWH
      WIUUMUUTUVDWBZXRXMXQUVJUULXMXQPZUUSUVCUURUVKYSUVCNZYEPZABCUBZOZUVCUUSUVKU
      VCWJZUVOUVCRUVPUVKYBWKVNABCUVCYDWAUVAWLWHYGUVNYFUVMABCYCUVLYEUVLYCXTXSYBU
      UJUUIUSWMVAVBWNWOWPWQWFWIWRUUMUUPYJUEZXRXMXQUULUVQXMXQUVQUULXMXQUUPYJUVKU
      UPPYHUUOYIUVKUUPWSXQUUOYIRXMUUPABCHIXNXNYDYIUUDXSHFUAIHFVJXTHXSFWTXSIHFXA
      UVBXBXCXDXEVSXFWFXGXHXIXJWHXKXLXF $.
      $( [27-Mar-2010] $)
  $}

  ${
    $d D x y z $.  $d F x y z $.  $d G x y z $.  $d R x y z $.  $d T x y z $.
    dualded2lem.1 $e |- D = ( dom_ ` T ) $.
    dualded2lem.2 $e |- C = ( cod_ ` T ) $.
    dualded2lem.3 $e |- R = ( o_ ` T ) $.
    $( Lemma for ~ dualded . $)
    dualded2lem $p |- (
       ( T e. Ded /\ ( F e. dom D /\ G e. dom D ) /\ ( D ` G ) = ( C ` F ) )
          -> ( F { <. <. x , y >. , z >.
          | ( <. y , x >. e. dom R /\ z = ( y R x ) ) } G ) = ( G R F ) ) $=
      ( cded wcel cdm wa cfv wceq cv cop co copab2 cxp wss wi wfun crn calg
      w3a dedalg eqid cmppfa syl 3simp2d sseld anim1d ssoprab2g ccnv cnvxp
      eqcomi eleq2i visset opelcnv bitri anbi1i oprabbii twsvbdop eqtr sseq2
      oprssoprvg eqcomd wfn oprex fnoprab2 fnfun ax-mp a1i 3simpl3 wb cmppfd
      3expb 3adant3 biimpar cvv elisset anim12i 3ad2ant2 adantr opelcnvg
      mpbird wrel relcnv dmoprabss6 bicomi dmeqi syl5eq eleqtrrd syl3ancOLD
      opreq2 opreq1 oprabval2g pm3.26 pm3.27 eqtrd 3exp1 com3r syl6bi mp2an
      mpcom 3imp ) GMNZHEOZNZIXLNZPZIEQHDQRZHIBSZASZTZFOZNZCSXQXRFUAZRZPZABCUBZ
      UAZIHFUAZRZYEXSXLXLUCZNZYCPZABCUBZUDZXKXOXPYHUEZUEZXKYDYKABCXKYAYJYCXKXTY
      IXSXKFUFZXTYIUDZFUGXLUDZXKGUHNYPYQYRUIGUJEFGXLXLUKZJLULUMUNUOUPUQYLXRXQTZ
      YINZYCPZABCUBZRZUUCXRXLNXQXLNPYCPABCUBZRZYMXKYOUEZUEZYKUUBABCYJUUAYCYJXSY
      IURZNUUAYIUUIXSUUIYIXLXLUSUTVAXQXRYIBVBZAVBZVCVDVEVFYCABCXLXLVGUUDUUFPYLU
      UERZUUHYLUUCUUEVHUULYMYEUUEUDZUUGYLUUEYEVIXKXOUUMYNXKXOUUMXPYHXKXOUUMUIZX
      PPZYFHIUUEUAZYGUUEUFZUUMHITZYEOZNZYFUUPRUUOUUQUUMUUTUIUUPYFHIUUEYEVJVKUUQ
      UUOUUEYIVLUUQABCXLXLYBUUEXQXRFVMZUUEUKZVNYIUUEVOVPVQXKXOUUMXPVRUUOUURXTUR
      ZUUSUUOUURUVCNZIHTXTNZUUNUVEXPXKXOUVEXPVSZUUMXKXMXNUVFDEFGHIXLYSJKLVTWAWB
      WCUUOHWDNZIWDNZPZUVDUVEVSUUNUVIXPXOXKUVIUUMXMUVGXNUVHHXLWEIXLWEWFWGWHHIWD
      WDXTWIUMWJUUNUUSUVCRZXPXKXOUVJUUMXKXOPZYTUVCNZYCPZABCUBZOZUVCUUSUVKUVCWKZ
      UVOUVCRUVPUVKXTWLVQABCUVCYBWDUVAWMUMYEUVNYDUVMABCYAUVLYCUVLYAXRXQXTUUKUUJ
      VCWNVEVFWOWPWBWHWQWRXMXNYGWDNZUUPYGRUUOABCHIXLXLYBYGUUEXQHFUAWDXRHXQFWSXQ
      IHFWTUVBXAUUNXMXPXOXKXMUUMXMXNXBWGWHUUNXNXPXOXKXNUUMXMXNXCWGWHUVQUUOIHFVM
      VQWRXDXEXFXGUMXHXIXJ $.
      $( [18-Apr-2010] $)
  $}

  ${
    dualcat2.1 $e |- D = ( dom_ ` T ) $.
    dualcat2.2 $e |- C = ( cod_ ` T ) $.
    dualcat2.3 $e |- J = ( id_ ` T ) $.
    dualcat2.4 $e |- R = ( o_ ` T ) $.
    ${
      $d C w x y z $.  $d R w x y z $.  $d T x y z $.
      $( The dual of a ` Alg ` is a ` Alg ` . $)
      dualalg $p |- ( T e. Alg ->
        <. <. C , D >. , <. J , { <. <. x , y >. , z >. |
          ( <. y , x >. e. dom R /\ z = ( y R x ) ) } >. >. e. Alg ) $=
        ( vw calg wcel cop cv cdm co wceq wa copab2 ccnv wb cvv visset
        opelcnvg bicomd mp2an a1i anbi1d oprabbidv opeq2d wf w3a wfun cxp wss
        crn feq2 biimpd feq3 3anim123d idd wi xpeq12 anidms sseq2 syl anim12d
        3simp2 3simp1 3simp3 3jca funss cnvss sqpsym sstr2 ssel syl6 mpi
        3ad2ant2 anim1d ssoprab2g wfn oprex twsvbdop fnoprab2 fnfun ax-mp sylc
        wrel relcnv cnvxp sseq1 eqcoms com12 syl6bi dmoprabss6 syl5com csn
        cuni cfv copab wral wex elrel opelcnv opswap fvelrn eleq1 imp 3adant1
        sylanc 3exp com14 a1d sylbi sneq cnveq unieqd fveq2d eleq1d imbi2d
        3imtr4d 19.23aivv ex pm2.43d com25 3imp r19.21aiv eqid fopab2 sylib
        frn sstrd eqcomi fveq2i syl5eq eqeq2d df-opr eqeq2i syl5bb
        dfoprab4spop rneqi syl5ss anim12i dcsda algi ccod_ fvex eqeltri cdom_
        cid_ 3pm3.2i co_ dmex cnvex oprabex2gpop pm3.2i isalg mpbird eqeltrd )
        GNOZDEPZHBQZAQZPZFRZOZCQZUVFUVGFSZTZUAZABCUBZPZPUVEHUVGUVFPZUVIUCZOZUVM
        UAZABCUBZPZPZNUVDUVPUWBUVEUVDUVOUWAHUVDUVNUVTABCUVDUVJUVSUVMUVJUVSUDZUV
        DUVGUEOZUVFUEOZUWDAUFZBUFZUWEUWFUAUVSUVJUVGUVFUEUEUVIUGUHUIUJUKULUMUMUV
        DUWCNOZDRZHRZDUNZUWJUWKEUNZUWKUWJHUNZUOZUWAUPZUWARZUWJUWJUQZURZUWAUSZUW
        JURZUOZUAZERZUWJTZUXDUWKEUNZUXDUWKDUNZUWKUXDHUNZUOZFUPZUVIUXDUXDUQZURZF
        USZUXDURZUOZUAZUXCUVDUXEUXPUWMUWLUWNUOZUXJUVIUWRURZUXMUWJURZUOZUAUXCUXE
        UXIUXQUXOUXTUXEUXFUWMUXGUWLUXHUWNUXEUXFUWMUXDUWJUWKEUTVAUXEUXGUWLUXDUWJ
        UWKDUTVAUXEUXHUWNUXDUWJUWKHVBVAVCUXEUXJUXJUXLUXRUXNUXSUXEUXJVDUXEUXKUWR
        TZUXLUXRVEUXEUYAUXDUWJUXDUWJVFVGUYAUXLUXRUXKUWRUVIVHVAVIUXEUXNUXSUXDUWJ
        UXMVHVAVCVJUXQUWOUXTUXBUXQUWLUWMUWNUWMUWLUWNVKUWMUWLUWNVLUWMUWLUWNVMVNU
        XTUWPUWSUXAUWAUVQUWROZUVMUAZABCUBZURUYDUPZUWPUXTUWAUYDVOUXTUVTUYCABCUXT
        UVSUYBUVMUXRUXJUVSUYBVEZUXSUXRUVRUWRUCZURZUYFUVIUWRVPZUYHUYGUWRURZUYFUW
        JVQUYHUYJUVRUWRURZUYFUVRUYGUWRVRUVRUWRUVQVSVTWAVIWBWCWDUYEUXTUYDUWRWEUY
        EABCUWJUWJUVLUYDUVFUVGFWFZUVMABCUWJUWJWGWHUWRUYDWIWJUJWKUVRWLZUXTUWSVEU
        VIWMZUXTUWQUVRTZUWSUYMUXRUXJUYOUWSVEZUXSUXRUYHUYPUYIUYGUWRTZUYHUYPVEUWJ
        UWJWNUYQUYHUYKUYPUYGUWRUVRVHUYOUYKUWSUYKUWSVEUVRUWQUVRUWQTUYKUWSUVRUWQU
        WRWOVAWPWQWRWJVIWBABCUVRUVLUEUYLWSWTWJUXTMQZUVROZUVKUYRXAZUCZXBZFXCZTZU
        AMCXDZUSZUWJUWTUXTVUFUXMUWJUXTUVRUXMVUEUNZVUFUXMURUXTVUCUXMOZMUVRXEVUGU
        XTVUHMUVRUXJUXRUXSUYSVUHVEZUXRUXJUXSVUIVEZUYMUXRUXJVUJVEVEUYNUYMUYSUXJU
        XSUXRVUHUYMUYSUXJUXSUXRVUHVEZVEZVEZUYMUYSUYSVUMVEZUYMUYSUAUYRUVQTZBXFAX
        FVUNABUYRUVRXGVUOVUNABVUOUVSUXJUXSUXRUVQXAZUCZXBZFXCZUXMOZVEZVEZVEZUYSV
        UMUVSVUOVVCUVSUVJVUOVVCVEUVGUVFUVIUWGUWHXHUVJVVCVUOVURUVHTZUVJVVCVEUVGU
        VFUWGUWHXIZUXSUVJUXJVVDVVAUVJUXJVVDVVAVEVEVEUXSUXRUXJVVDUVJVUTUXJVVDUVJ
        VUTVEVEVEUXRUXJVVDUVJVUTUXJVURUVIOZVUTUXJVVDUVJUOVURFXJUXJVVDUVJVLVVDUV
        JVVFUXJVVDUVJVVFUVJVVFVEUVHVURUVHVURTUVJVVFUVHVURUVIXKVAWPXLXMXNXOUJXPU
        JXPWJXQXRWQUYRUVQUVRXKVUOVULVVBUXJVUOVUKVVAUXSVUOVUHVUTUXRVUOVUCVUSUXMV
        UOVUBVURFVUOVUAVUQVUOUYTVUPTVUAVUQTUYRUVQXSUYTVUPXTVIYAYBYCYDYDYDYEYFVI
        YGYHYIWJWQYJYKMCUVRUXMVUCVUEVUEYLYMYNUVRUXMVUEYOVIUXJUXRUXSVMYPUWAVUEUY
        MUWAVUETUYNUVMVUDABCMUVRUVQUYRTZUVKUVHFXCZTVUDUVMVVGVVHVUCUVKVVGVUSVUCV
        VHVVGVURVUBFVVGVUQVUAVVGVUPUYTTVUQVUATUVQUYRXSVUPUYTXTVIYAYBUVHVURFVURU
        VHVVEYQYRYSYTUVLVVHUVKUVFUVGFUUAUUBUUCUUDWJUUEUUFVNUUGVTDEGIJUUHDEFGHUX
        DUWKIJKLUXDYLUWKYLZUUIWKUVDDUEOZEUEOZHUEOZUOZUWAUEOZUAZUWIUXCUDVVOUVDVV
        MVVNVVJVVKVVLDGUUJXCUEJGUUJUUKUULEGUUMXCUEIGUUMUUKUULHGUUNXCUEKGUUNUUKU
        ULUUOUVRUEOUYMVVNUVIFFGUUPXCUELGUUPUUKUULUUQUURUYNABCUVLUEUVRUUSUIUUTUJ
        UEUEEDUWAUEUEHUWJUWKUWJYLVVIUVAVIUVBUVC $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d C a f g $.  $d C f g x y z $.  $d D a f g $.  $d D f g x y z $.
      $d J a $.  $d R f g x y z $.  $d T a f g $.  $d T f g x y z $.
      $( The dual of a deductive system is a deductive system. $)
      dualded $p |- ( T e. Ded ->
        <. <. C , D >. , <. J , { <. <. x , y >. , z >. |
        ( <. y , x >. e. dom R /\ z = ( y R x ) ) } >. >. e. Ded ) $=
        ( va vg vf cded wcel cop cv cdm co wceq wa copab2 calg cfv wral wb w3a
        wi dedalg dualalg syl eqid idosd ancomd r19.21aiva ccnv wrel relcnv
        cvv oprex dmoprabss6 ax-mp a1i visset opelcnv bicomi anbi1i oprabbii
        dmeqi syl5eq eleq2d dcsda eleq2 anbi12d imbi1d cmppfd 3expib syl5bir
        eqcoms 3syl pm2.43i com12 ancoms impcom eqcom 3bitrd bitrd ex
        r19.21aivv 3jca dualded2lem fveq2d simpll eqcomd biimpa adantr biimpi
        adantl syl3ancOLD codcmpd 3exp imbi12d imbi2d mpbiri com3l eqtrd exp31 id
        domcmpd jca32 isded ccod_ cdom_ cid_ eleq1i 3anbi123i fvex mpbir3an
        cxp wss ssexg wfun crn cmppfa 3simp2d sseld anim1d ssoprab2g cnvxp
        eleq2i bitr3i eqeltri dmex xpex relxp oprabex2gpop mp2an sylancl
        sylancr mpbird ) GPQZDERHBSZASZRZFTZQZCSUUDUUEFUAZUBZUCZABCUDZRRZPQZUUM
        UEQZMSZHUFZDUFUUPUBZUUQEUFUUPUBZUCZMHTZUGZNSZOSZRZUULTZQZUVCDUFZUVDEUFZ
        UBZUHZNDTZUGOUVLUGZUIZUVJUVCUVDUULUAZDUFZUVDDUFZUBZUJZNUVLUGOUVLUGZUVJU
        VOEUFZUVCEUFZUBZUJZNUVLUGOUVLUGZUCUCZUUCUVNUVTUWEUUCUUOUVBUVMUUCGUEQZUU
        OGUKZABCDEFGHIJKLULUMUUCUUTMUVAUUCUUPUVAQUCUUSUURUUPDEGHUVAUVAUNZIKJUOU
        PUQUUCUVKONUVLUVLUUCUVDUVLQZUVCUVLQZUCZUVKUUCUWLUCZUVGUVEUUGURZQZUVJUWM
        UVFUWNUVEUWMUUEUUDRZUWNQZUUJUCZABCUDZTZUWNUVFUWTUWNUBZUWMUWNUSUXAUUGUTA
        BCUWNUUIVAUUDUUEFVBVCVDVEUULUWSUUKUWRABCUUHUWQUUJUWQUUHUUEUUDUUGAVFZBVF
        ZVGVHVIVJVKVLVMUWMUWOUVDUVCRUUGQZUVIUVHUBZUVJUWOUXDUHUWMUVCUVDUUGNVFOVF
        VGVEUWLUUCUXDUXEUHZUWKUWJUUCUXFUJUUCUWKUWJUCZUXFUUCUXGUXFUJZUUCUWGETZUV
        LUBZUUCUXHUJZUWHDEGIJVNZUXKUVLUXIUVLUXIUBZUXHUVCUXIQZUVDUXIQZUCZUXFUJUU
        CUXMUXGUXPUXFUXMUWKUXNUWJUXOUVLUXIUVCVOZUVLUXIUVDVOZVPVQUUCUXNUXOUXFDEF
        GUVCUVDUXIUXIUNZIJLVRVSVTWAWBWCWDWEWFUXEUVJUHUWMUVIUVHWGVEWHWIWJWKWLUUC
        UVSONUVLUVLUUCUWLUVJUVRUWMUVJUCZUVPUVDUVCFUAZDUFZUVQUUCUXPUXEUVPUYBUBUX
        TUUCUXPUXEUIZUVOUYADABCDEFGUVCUVDIJLWMZWNUUCUWLUVJWOZUWMUXPUVJUWMUXOUXN
        UUCUWLUXOUXNUCZUUCUWGUWLUYFUHUWHUWGUWJUXOUWKUXNUWGUVLUXIUVDUWGUXIUVLUXL
        WPZVMUWGUVLUXIUVCUYGVMVPUMWQUPWRZUVJUXEUWMUVJUXEUVHUVIWGWSWTZXAUVJUWMUY
        BUVQUBZUWMUYJUJUVIUVHUWMUXEUYJUWLUUCUXEUYJUJZUWKUWJUUCUYKUJUUCUWKUWJUYK
        UUCUWKUWJUYKUJZUJZUUCUWGUXJUUCUYMUJZUWHUXLUXJUYNUUCUXNUXOUYKUJZUJZUJZUU
        CUXNUXOUYKDEFGUVCUVDUXIUXSIJLXBXCUYNUYQUHUVLUXIUXMUYMUYPUUCUXMUWKUXNUYL
        UYOUXQUXMUWJUXOUYKUXRVQXDXEWAXFWBWCXGWFWFWDWAWFXHXIWKUUCUWDONUVLUVLUUCU
        WLUVJUWCUXTUWAUYAEUFZUWBUUCUXPUXEUWAUYRUBUXTUYCUVOUYAEUYDWNUYEUYHUYIXAU
        VJUWMUYRUWBUBZUWMUYSUJUVIUVHUWMUXEUYSUWLUUCUXEUYSUJZUWKUWJUUCUYTUJUUCUW
        KUWJUYTUUCUWKUWJUYTUJZUJZUUCUWGUXJUUCVUBUJUWHUXLUXJVUBUXNUXOUYTUJZUJUUC
        UXJUWKUXNVUAVUCUXJUVLUXIUVCUXMUVLUXIUXMXJWAZVMUXJUWJUXOUYTUXJUVLUXIUVDV
        UDVMVQXDUUCUXNUXOUYTDEFGUVCUVDUXIUXSIJLXKXCVTWBWCXGWFWFWDWAWFXHXIWKXLDV
        AQZEVAQZHVAQZUIZUULVAQZUUNUWFUHUUCVAVAEDUULONVAVAHUVLUVAMUVLUNUWIXMVUHG
        XNUFZVAQZGXOUFZVAQZGXPUFZVAQZVUEVUKVUFVUMVUGVUODVUJVAJXQEVULVAIXQHVUNVA
        KXQXRGXNXSGXOXSZGXPXSXTUULUUFUXIUXIYAZQZUUJUCZABCUDZYBVUTVAQVUIUUCUULVU
        TVAYCUUCUUKVUSABCUUCUUHVURUUJUUCUUGVUQUUFUUCFYDZUUGVUQYBZFYEUXIYBZUUCUW
        GVVAVVBVVCUIUWHEFGUXIUXSILYFUMYGYHYIYJVUTUWPVUQQZUUJUCZABCUDZVAVUSVVEAB
        CVURVVDUUJVURUWPVUQURZQVVDUUEUUDVUQUXBUXCVGVVGVUQUWPUXIUXIYKYLYMVIVJVUQ
        VAQVUQUSVVFVAQUXIUXIEEVULVAIVUPYNYOZVVHYPUXIUXIYQABCUUIVAVUQYRYSYNYTUUA
        UUB $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d C a f g h x y z $.  $d D a f g h x y z $.  $d J a f x y z $.
      $d R a f g h x y z $.  $d T a f g h x y z $.
      $( The dual of a category is a category.  Joy of cats 3.5 $)
      dualcat2 $p |- ( T e. Cat ->
      <. <. C , D >. , <. J , { <. <. x , y >. , z >. |
          ( <. y , x >. e. dom R /\ z = ( y R x ) ) } >. >. e. Cat ) $=
        ( vh vg vf va ccat wcel cop cv cdm co wceq wa copab2 cded cfv wi wral
        catded dualded syl w3a calg dedalg dcsda eleq2 3anbi123d biimprd eqid
        cmpasso ex syl9 3syl pm2.43i imp com12 eqcoms 3exp2 com24 imp32 imp31
        3simp12 wb anbi12d biimpcd com3l adantl impcom 3adant2 3adant3 eqcom
        biimpi adantr 3ad2ant3 3jca 3exp imp41 dualcat2lem opreq1 simplll
        3simpl2 biimpd 3ad2ant1 cdom_ dmeqi cmpmorp syl6 com13 3imp jca id
        ad2antll biimpa adantld a1d 3exp1 codcmpc eqtr4d syl3ancOLD sylan9eqr
        mpdan eqcomd eleq2d ad2antrr opreq2 pm3.26 sylc domcmpc simpll syl2an
        ad2antrl eqtrd 3eqtr4rd exp31 r19.21aiv r19.21aivv cid_ jdmo adantrr
        pm3.27 idosc simplrl sylanc pm3.27d cmpidb simprl cmpida 3expib anbi2d
        imbi1d simplrr eqtr3 pm3.26d sylan eqeq1d pm5.74da bitrd mpbird jca31
        cvv iscat ccod_ eleq1i 3anbi123i fvex mpbir3an cxp wss ssexg wfun crn
        cmppfc 3simp2d sseld anim1d ssoprab2g dmex eqeltri oprabex2 ccnv cnvxp
        eqcomi eleq2i visset opelcnv bitri anbi1i oprabbii twsvbdop mpbir
        sylancl sylancr ) GQRZDESHBTZATZSZFUAZRZCTUWEUWFFUBZUCZUDZABCUEZSSZQRZU
        WNUFRZMTZDUGZNTZEUGZUCZUWSDUGZOTZEUGZUCZUDZUWQUWSUXCUWMUBZUWMUBZUWQUWSU
        WMUBZUXCUWMUBZUCZUHZMDUAZUIZNUXMUIOUXMUIZUDUXDPTZUCZUXPHUGZUXCUWMUBZUXC
        UCZUHZOUXMUIPHUAZUIZUXCDUGZUXPUCZUXCUXRUWMUBZUXCUCZUHZOUXMUIPUYBUIZUDZU
        DZUWDUWPUXOUYJUWDGUFRZUWPGUJZABCDEFGHIJKLUKULUWDUXNONUXMUXMUWDUXCUXMRZU
        WSUXMRZUDZUXNUWDUYPUDZUXLMUXMUYQUWQUXMRZUXFUXKUYQUYRUDZUXFUDZUXCUWSUWQF
        UBZFUBZUXCUWSFUBZUWQFUBZUXJUXHUYQUYRUXFVUBVUDUCZUWDUYNUYOUYRUXFVUEUHZUH
        UWDUYRUYOUYNVUFUWDUYRUYOUYNVUFUXFUWDUYRUYOUYNUMZUDZVUEUXAUXEVUHVUEUHZUX
        EVUIUHUWTUWRUXEUWTUWRUCZVUIVUJVUIUHUXDUXBUXDUXBUCZVUJVUIVUHVUKVUJUDZVUE
        UWDVUGVULVUEUHZUWDVUGVUMUHZUWDUYLGUNRZUWDVUNUHZUYMGUOZVUOEUAZUXMUCZVUPD
        EGIJUPZVUSVUGUWQVURRZUWSVURRZUXCVURRZUMZUWDVUMVUSVVDVUGVUSVVAUYRVVBUYOV
        VCUYNVURUXMUWQUQVURUXMUWSUQVURUXMUXCUQURUSUWDVVDVUMDEFGUWQUWSUXCVURVURU
        TZIJLVAVBVCULVDVEVFVGVBVHVGVHVFVGVIVJVKVLUYTUXIVUAUCZUXJVUBUCUYTUWDVVAV
        VBUDZVUJUMZVVFUWDUYPUYRUXFVVHUWDUYPUYRUXFVVHUHUHZUHZUWDUYLVUOUWDVVJUHZU
        YMVUQVUOVUSVVKVUTVUSUWDUYPVVIVUSUWDUYPUMZUYRUXFVVHVVLUYRUXFUMZUWDVVGVUJ
        VUSUWDUYPUYRUXFVMVVLUYRVVGUXFVVLUYRVVGVUSUYPUYRVVGUHZUWDUYPVUSVVNUYOVUS
        VVNUHUYNUYRUYOVUSVVGUYRUYOVUSVVGUHVUSUYRUYOUDZVVGVVOVVGVNUXMVURUXMVURUC
        ZUYRVVAUYOVVBUXMVURUWQUQZUXMVURUWSUQZVOVHVPVBVQVRVSVTVFWAUXFVVLVUJUYRUX
        AVUJUXEUXAVUJUWRUWTWBWCZWDWEWFWGWGULVDVEWHABCDEFGUWQUWSIJLWIULVVFUYTUXJ
        VUAUXCUWMUBZVUBUXIVUAUXCUWMWJUWDVUAVURRZVVCUDZUXDVUADUGZUCVVTVUBUCUYTAB
        CDEFGVUAUXCIJLWIUWDUYPUYRUXFWKZUWDUYPUYRUXFVWBUWDUYPUYRUXFVWBUHUHZUHZUW
        DUYLVUOUWDVWFUHZUYMVUQVUOVUSVWGVUTVUSUWDUYPVWEVVLUYRUXFVWBVVMVWAVVCVVLU
        YRUXFVWAUXFUYRVVLVWAUXAUYRVVLVWAUHUHZUXEVWHUWTUWRVVLUYRVUJVWAVVLUYRUWDV
        VAVVBUMZVUJVWAUHVVLUYRVWIVVLUYRUDZUWDVVAVVBVUSUWDUYPUYRWLZVVLUYRVVAVUSU
        WDUYRVVAUHZUYPVWLUXMVURVVPUYRVVAVVQWMVHWNVFVVLVVBUYRVUSUYPVVBUWDUYPVUSV
        VBUYOVUSVVBUHUYNVUSUYOVVBUYOVVBUHUXMVURVVPUYOVVBVVRWMVHZVGVRVSVTWDWFVBD
        EFGUWQUWSVUREGWOUGZIWPZIJLWQWRWSVHWDWSWTVVLUYRVVCUXFVUSUYPVVCUWDUYPVUSV
        VCUYNVUSVVCUHUYOVUSUYNVVCUYNVVCUHUXMVURVVPUYNVVCUXMVURUXCUQWMVHVGWDVSVT
        WNXAWGWGULVDVEWHUYTUXDUXBVWCUXEVUKUYSUXAVUKUXDUXBVUKXBVHXCUXFUYSVWCUXBU
        CZUXAUYSVWPUHZUXEVWQUWTUWRUYSVUJVWPUYSVWIVUJVWPUHUWDUYPUYRVWIUWDUYPUYRV
        WIUHUHZUWDUYLVUOUWDVWRUHZUYMVUQVUOVUSVWSVUTVUSUWDUYPUYRVWIVWJUWDVVAVVBV
        WKVVLUYRVVAVUSUWDUYRVVAVNZUYPVWTUXMVURVVQVHWNXDVVLVVBUYRVUSUWDUYPVVBVUS
        UYPVVBUHUWDVUSUYOVVBUYNVWMXEXFWTWDWFXGULVDVEVLDEFGUWQUWSVURVVEIJLXHULVG
        VHWDVSXIXJXKXLUYTUXGVUCUCZUXHVUDUCUWDVVBVVCUDVUKVXAUYTABCDEFGUWSUXCIJLW
        IVWDUYTVVBVVCUYQVVBUYRUXFUYPUWDVVBUYOUWDVVBUHUYNUWDUYOVVBUWDUYLVUOUYOVV
        BVNUYMVUQVUOUXMVURUWSVUOVURUXMVUTXMZXNVDVPVRVSZXOUYQVVCUYRUXFUYPUWDVVCU
        YNUWDVVCUHZUYOUWDUYNVVCUWDUXMVURUXCUWDUYLVUOVVPUYMVUQVXBVDZXNZVPZWDVSZX
        OXAUXEVUKUYSUXAUXEVUKUXBUXDWBWCZXCZXJVXAUYTUXHUWQVUCUWMUBZVUDUXGVUCUWQU
        WMXPUWDVVAVUCVURRZUDVUCEUGZUWRUCVXKVUDUCUYTABCDEFGUWQVUCIJLWIVWDUYTVVAV
        XLUYSVVAUXFUYQUYRVVAUWDVWLUYPUWDUYRVVAUWDUXMVURUWQVXEXNWMWDVFWDUWDVVBVV
        CUMZVUKVXLUYTDEFGUWSUXCVURVWOIJLWQUYQVXNUYRUXFUYQUWDVVBVVCUWDUYPXQVXCVX
        HWFXOVXJXRXAUYTVXMUWTUWRVXNVUKVXMUWTUCZUYSUXFVXNVUKVXODEFGUWSUXCVURVVEI
        JLXSVFUYSUWDVVBVVCUWDUYPUYRXTUYQVVBUYRVXCWDUYQVVCUYRVXHWDWFUXEVUKUXAVXI
        VRYAUXAVUJUYSUXEVVSYBYCXJXKXLYDYEYFVBYGUWDUYCUYIUWDUYAPOUYBUXMUWDUXPUYB
        RZUYNUDZUXQUXTUWDVXQUDZUXQUDZUXSUXCUXRFUBZUXCUWDUXRVURRZVVCUDUXDUXRDUGZ
        UCUXSVXTUCVXSABCDEFGUXRUXCIJLWIUWDVXQUXQXTZVXSVYAVVCVXRVYAUXQUWDVXPVYAU
        YNUXPGHVURUYBVWOHGYHUGZKWPKYIZYJWDVXRVVCUXQVXQUWDVVCUYNVXDVXPVXGVRVSWDX
        AVXSUXDUXPVYBVXRUXQYKVXSUXREUGZUXPUCZVYBUXPUCZUWDVXPVYGVYHUDVXSUXPDEGHU
        YBUYBUTZIKJYLZVYCUWDVXPUYNUXQYMYNYOXIXJVXRUXQVXTUXCUCZUWDVXPVVCUXQVYKUH
        VXRUXPEFGUXCHVURUYBVVEIVYIKLYPUWDVXQXQUWDVXPUYNYQVXQUWDVVCUYNVXDVXPUWDU
        YNVVCUWDUXMVURUXCUWDVURUXMUWDVUOVUSUWDUYLVUOUYMVUQULVUTULXMXNVPVRVSXJVF
        YCYEYGUWDUYHPOUYBUXMUWDVXQUYHUHZVXPVVCUDZUYEUXRUXCFUBZUXCUCZUHZUHZUWDVX
        PVVCVYPUXPDEFGUXCHVURUYBVVEIVYIKLJYRYSUWDVYLVYMUYHUHVYQUWDVXQVYMUYHUWDU
        YNVVCVXPVXFYTUUAUWDVYMUYHVYPUWDVYMUDZUYEUYGVYOVYRUYEUDZUYFVYNUXCUWDVVCV
        YAUDVYFUYDUCZUYFVYNUCVYSABCDEFGUXCUXRIJLWIUWDVYMUYEXTVYSVVCVYAUWDVXPVVC
        UYEUUBVYRVYAUYEUWDVXPVYAVVCVYEYJWDXAVYGUYEVYTVYRVYFUYDUXPUUCUWDVXPVYGVV
        CUWDVXPUDVYGVYHVYJUUDYJUUEXJUUFUUGUUGUUHUUIYGXAUUJDUUKRZEUUKRZHUUKRZUMZ
        UWMUUKRZUWOUYKVNUWDUUKUUKEDUWMONMUUKUUKHUXMUYBPUXMUTVYIUULWUDGUUMUGZUUK
        RZVWNUUKRZVYDUUKRZWUAWUGWUBWUHWUCWUIDWUFUUKJUUNEVWNUUKIUUNHVYDUUKKUUNUU
        OGUUMUUPGWOUUPZGYHUUPUUQUWMUWGVURVURUURZRZUWKUDZABCUEZUUSWUNUUKRZWUEUWD
        UWMWUNUUKUUTUWDUWLWUMABCUWDUWIWULUWKUWDUWHWUKUWGUWDFUVAUWHWUKUUSFUVBVUR
        UUSEFGVURVVEILUVCUVDUVEUVFUVGWUOUWFVURRUWEVURRUDUWKUDABCUEZUUKRZABCVURV
        URUWJWUPVURVWNUAUUKVWOVWNWUJUVHUVIZWURWUPUTUVJWUOUWFUWESWUKRZUWKUDZABCU
        EZUUKRWUQWUNWVAUUKWUMWUTABCWULWUSUWKWULUWGWUKUVKZRWUSWUKWVBUWGWVBWUKVUR
        VURUVLUVMUVNUWEUWFWUKBUVOAUVOUVPUVQUVRUVSUUNWVAWUPUUKUWKABCVURVURUVTUUN
        UVQUWAUWBUWCUUI $.
        $( [4-Apr-2010] $)
    $}
  $}


