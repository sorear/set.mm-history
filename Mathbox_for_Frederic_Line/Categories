$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Categories
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Cat $. $( The class of all categories. $)

  $( Extend class notation with the class of categories. $)
  ccat $a class Cat $.

  ${
    $d x d c j r f g h a $.
    $( A category is a deductive system where composition is associative, and
       identities are neutral elements.  A category is an abstraction of a
       structure (a group, a topology, an order...)  Category theory consists
       in finding new formulation of the concepts associated to those
       structures (product, substructure...) using morphisms instead of the
       belonging relation.  That trick has the interesting property that
       heterogeneous structures like topologies or groups for instance become
       comparable.  (Note: in category theory morphisms are also called
       arrows.) $)
    df-cat $a |- Cat = { x | E. d E. c E. j E. r
    ( x = <. <. d , c >. , <. j , r >. >. /\
           ( ( <. <. d , c >. , <. j , r >. >. e. Ded /\
           A. f e. dom d A. g e. dom d A. h e. dom d
              (
                ( ( d ` h ) = ( c ` g ) /\ ( d ` g ) = ( c ` f ) )
              ->
                ( h r ( g r f ) ) = ( ( h r g ) r f )
               ) ) /\
          ( A. a e. dom j A. f e. dom d ( ( c ` f ) = a ->
             ( ( j ` a ) r f ) = f ) /\
          A. a e. dom j A. f e. dom d ( ( d ` f ) = a ->
             ( f r ( j ` a ) ) = f ) ) ) ) } $.
  $}

  ${
    $d C a c d f g h j r $.  $d D a c d f g h j r $.  $d J a c d f j r $.
    $d M c d j r f $.  $d O c d j r $.  $d R a c d f g h j r $.
    $d a c d f g h j r x $.
    iscat.1 $e |- M = dom D $.
    iscat.2 $e |- O = dom J $.
    $( The predicate "is a category".  (Contributed by FL, 24-Oct-2007.) $)
    iscat $p |- ( ( ( D e. A /\ C e. B /\ J e. F ) /\ R e. G )
      -> ( <. <. D , C >. , <. J , R >. >. e. Cat
        <-> ( ( <. <. D , C >. , <. J , R >. >. e. Ded /\
           A. f e. M A. g e. M A. h e. M
              (
                ( ( D ` h ) = ( C ` g ) /\ ( D ` g ) = ( C ` f ) )
              ->
                ( h R ( g R f ) ) = ( ( h R g ) R f )
               ) ) /\
          ( A. a e. O A. f e. M ( ( C ` f ) = a ->
             ( ( J ` a ) R f ) = f ) /\
          A. a e. O A. f e. M ( ( D ` f ) = a ->
             ( f R ( J ` a ) ) = f ) ) ) ) ) $=
      ( wcel wceq wa wral vx vd vc vj vr cop ccat cv cded cfv co wi cdm wex cab
      w3a df-cat eleq2i opeq1 opeq1d eleq1d syl6eqr eleq2d fveq1 eqeq1d anbi12d
      dmeq imbi1d imbi12d ralbidv2 raleqdv ralbidv opeq2 eqeq2d 2ralbidv anbi1d
      opeq2d opreq1d imbi2d opreq2d opreq eqtrd eqeq12d elo syl5bb ) DCUFZKEUFZ
      UFZUGQWHUAUHUBUHZUCUHZUFZUDUHZUEUHZUFZUFZRWOUIQZHUHZWIUJZGUHZWJUJZRZWSWIU
      JZFUHZWJUJZRZSZWQWSXCWMUKZWMUKZWQWSWMUKZXCWMUKZRZULZHWIUMZTZGXMTZFXMTZSZX
      DNUHZRZXRWLUJZXCWMUKZXCRZULZFXMTZNWLUMZTZXCWIUJZXRRZXCXTWMUKZXCRZULZFXMTZ
      NYETZSZSZSUEUNUDUNUCUNUBUNUAUOZQDAQCBQKIQUPEJQSWHUIQZWQDUJZWSCUJZRZWSDUJZ
      XCCUJZRZSZWQWSXCEUKZEUKZWQWSEUKZXCEUKZRZULZHLTZGLTFLTZSZUUBXRRZXRKUJZXCEU
      KZXCRZULZFLTNMTZXCDUJZXRRZXCUUOEUKZXCRZULZFLTNMTZSZSZUGYPWHUAFGHUDUENUCUB
      UQURYODWJUFZWNUFZUIQZYRWTRZUUAXDRZSZXKULZHLTZGLTZFLTZSZYCFLTZNYETZUVAYJUL
      ZFLTZNYETZSZSWFWNUFZUIQZUUDXKULZHLTZGLTFLTZSZUUNYBULZFLTZNYETZUWCSZSWFKWM
      UFZUFZUIQZUWISZUUNUUOXCWMUKZXCRZULZFLTZNMTZUVAXCUUOWMUKZXCRZULZFLTZNMTZSZ
      SUVGUAUBUCUEUDDCKEABIJWIDRZXQUVRYNUWDUXJWPUVJXPUVQUXJWOUVIUIUXJWKUVHWNWID
      WJUSUTVAUXJXOUVPFXMLUXJXCXMQZXCLQZXOUVPUXJXMLXCUXJXMDUMLWIDVGOVBZVCZUXJXN
      UVOGXMLUXJWSXMQWSLQXNUVOUXJXMLWSUXMVCUXJXLUVNHXMLUXJWQXMQWQLQXLUVNUXJXMLW
      QUXMVCUXJXFUVMXKUXJXAUVKXEUVLUXJWRYRWTWQWIDVDVEUXJXBUUAXDWSWIDVDVEVFVHVIV
      JVIVJVIVJVFUXJYFUVTYMUWCUXJYDUVSNYEUXJYCFXMLUXMVKVLUXJYLUWBNYEUXJYKUWAFXM
      LUXJUXKUXLYKUWAUXNUXJYHUVAYJUXJYGUUTXRXCWIDVDVEVHVIVJVLVFVFWJCRZUVRUWJUWD
      UWNUXOUVJUWFUVQUWIUXOUVIUWEUIUXOUVHWFWNWJCDVMUTVAUXOUVOUWHFGLLUXOUVNUWGHL
      UXOUVMUUDXKUXOUVKYTUVLUUCUXOWTYSYRWSWJCVDVNUXOXDUUBUUAXCWJCVDZVNVFVHVLVOV
      FUXOUVTUWMUWCUXOYCUWKNFYELUXOXSUUNYBUXOXDUUBXRUXPVEVHVOVPVFWLKRZUWJUWRUWN
      UXIUXQUWFUWQUWIUXQUWEUWPUIUXQWNUWOWFWLKWMUSVQVAVPUXQUWMUXCUWCUXHUXQUWLUXB
      NYEMUXQXRYEQZXRMQZUWLUXBUXQYEMXRUXQYEKUMMWLKVGPVBVCZUXQUWKUXAFLUXQYBUWTUU
      NUXQYAUWSXCUXQXTUUOXCWMXRWLKVDZVRVEVSVLVIVJUXQUWBUXGNYEMUXQUXRUXSUWBUXGUX
      TUXQUWAUXFFLUXQYJUXEUVAUXQYIUXDXCUXQXTUUOXCWMUYAVTVEVSVLVIVJVFVFWMERZUWRU
      UMUXIUVFUYBUWQYQUWIUULUYBUWPWHUIUYBUWOWGWFWMEKVMVQVAUYBUWHUUKFGLLUYBUWGUU
      JHLUYBXKUUIUUDUYBXHUUFXJUUHUYBXHWQUUEWMUKUUFUYBXGUUEWQWMWSXCWMEWAVTWQUUEW
      MEWAWBUYBXJXIXCEUKUUHXIXCWMEWAUYBXIUUGXCEWQWSWMEWAVRWBWCVSVLVOVFUYBUXCUUS
      UXHUVEUYBUXAUURNFMLUYBUWTUUQUUNUYBUWSUUPXCUUOXCWMEWAVEVSVOUYBUXFUVDNFMLUY
      BUXEUVCUVAUYBUXDUVBXCXCUUOWMEWAVEVSVOVFVFWDWE $.
      $( [24-Oct-2007] $)
  $}

  ${
    $d C a c d f g h j r $.  $d D a c d f g h j r $.  $d J a c d f j r $.
    $d M c d j r f $.  $d O c d j r $.  $d R a c d f g h j r $.
    $d T c d j r x $.  $d a c d f g h j r x $.
    cati.1 $e |- D = ( dom_ ` T ) $.
    cati.2 $e |- C = ( cod_ ` T ) $.
    cati.3 $e |- J = ( id_ ` T ) $.
    cati.4 $e |- R = ( o_ ` T ) $.
    cati.5 $e |- M = dom D $.
    cati.6 $e |- O = dom J $.
    $( Definitional properties of a category.  (Contributed by FL,
       24-Oct-2007.) $)
    cati $p |- ( T e. Cat
       -> ( ( <. <. D , C >. , <. J , R >. >. e. Ded /\
           A. f e. M A. g e. M A. h e. M
              (
                ( ( D ` h ) = ( C ` g ) /\ ( D ` g ) = ( C ` f ) )
              ->
                ( h R ( g R f ) ) = ( ( h R g ) R f )
               ) ) /\
          ( A. a e. O A. f e. M ( ( C ` f ) = a ->
             ( ( J ` a ) R f ) = f ) /\
          A. a e. O A. f e. M ( ( D ` f ) = a ->
             ( f R ( J ` a ) ) = f ) ) ) ) $=
      ( wceq wa wral vx vd vc vj vr ccat wcel cv cop cded cfv co wi cdm wex cab
      df-cat eleq2i c1st domval eqcomi eqeq2i opeq1d eleq1d dmeq syl6eqr eleq2d
      wb opeq1 fveq1 eqeq1d anbi12d imbi12d ralbidv2 raleqdv ralbidv sylbi c2nd
      imbi1d codval eqeq2d 2ralbidv anbi1d opeq2d opreq1d imbi2d opreq2d cmpval
      opeq2 idval opreq eqtrd eqeq12d eloi ) DUFUGDUAUHUBUHZUCUHZUIZUDUHZUEUHZU
      IZUIZRXAUJUGZGUHZWOUKZFUHZWPUKZRZXEWOUKZEUHZWPUKZRZSZXCXEXIWSULZWSULZXCXE
      WSULZXIWSULZRZUMZGWOUNZTZFXSTZEXSTZSZXJKUHZRZYDWRUKZXIWSULZXIRZUMZEXSTZKW
      RUNZTZXIWOUKZYDRZXIYFWSULZXIRZUMZEXSTZKYKTZSZSZSUEUOUDUOUCUOUBUOUAUPZUGBA
      UIZHCUIZUIZUJUGZXCBUKZXEAUKZRZXEBUKZXIAUKZRZSZXCXEXICULZCULZXCXECULZXICUL
      ZRZUMZGITZFITEITZSZUUKYDRZYDHUKZXICULZXIRZUMZEITKJTZXIBUKZYDRZXIUVDCULZXI
      RZUMZEITKJTZSZSZUFUUBDUAEFGUDUEKUCUBUQURUUABWPUIZWTUIZUJUGZUUGXFRZUUJXJRZ
      SZXQUMZGITZFITZEITZSZYIEITZKYKTZUVJYPUMZEITZKYKTZSZSZUUCWTUIZUJUGZUUMXQUM
      ZGITZFITEITZSZUVCYHUMZEITZKYKTZUWLSZSZUUCHWSUIZUIZUJUGZUWSSZUVCUVDXIWSULZ
      XIRZUMZEITZKJTZUVJXIUVDWSULZXIRZUMZEITZKJTZSZSZUVPUAUBUCUEUDDWODUSUKZUSUK
      ZRWOBRZUUAUWNVHUYCBWOBUYCBDLUTVAVBUYDYCUWGYTUWMUYDXBUVSYBUWFUYDXAUVRUJUYD
      WQUVQWTWOBWPVIVCVDUYDYAUWEEXSIUYDXIXSUGZXIIUGZYAUWEUYDXSIXIUYDXSBUNIWOBVE
      PVFZVGZUYDXTUWDFXSIUYDXEXSUGXEIUGXTUWDUYDXSIXEUYGVGUYDXRUWCGXSIUYDXCXSUGX
      CIUGXRUWCUYDXSIXCUYGVGUYDXLUWBXQUYDXGUVTXKUWAUYDXDUUGXFXCWOBVJVKUYDXHUUJX
      JXEWOBVJVKVLVSVMVNVMVNVMVNVLUYDYLUWIYSUWLUYDYJUWHKYKUYDYIEXSIUYGVOVPUYDYR
      UWKKYKUYDYQUWJEXSIUYDUYEUYFYQUWJUYHUYDYNUVJYPUYDYMUVIYDXIWOBVJVKVSVMVNVPV
      LVLVQWPUYBVRUKZRWPARZUWNUXEVHUYIAWPAUYIADMVTVAVBUYJUWGUWTUWMUXDUYJUVSUWPU
      WFUWSUYJUVRUWOUJUYJUVQUUCWTWPABWIVCVDUYJUWDUWREFIIUYJUWCUWQGIUYJUWBUUMXQU
      YJUVTUUIUWAUULUYJXFUUHUUGXEWPAVJWAUYJXJUUKUUJXIWPAVJZWAVLVSVPWBVLUYJUWIUX
      CUWLUYJYIUXAKEYKIUYJYEUVCYHUYJXJUUKYDUYKVKVSWBWCVLVQWRDVRUKZUSUKZRWRHRZUX
      EUYAVHUYMHWRHUYMDHNWJVAVBUYNUWTUXIUXDUXTUYNUWPUXHUWSUYNUWOUXGUJUYNWTUXFUU
      CWRHWSVIWDVDWCUYNUXCUXNUWLUXSUYNUXBUXMKYKJUYNYDYKUGZYDJUGZUXBUXMUYNYKJYDU
      YNYKHUNJWRHVEQVFVGZUYNUXAUXLEIUYNYHUXKUVCUYNYGUXJXIUYNYFUVDXIWSYDWRHVJZWE
      VKWFVPVMVNUYNUWKUXRKYKJUYNUYOUYPUWKUXRUYQUYNUWJUXQEIUYNYPUXPUVJUYNYOUXOXI
      UYNYFUVDXIWSUYRWGVKWFVPVMVNVLVLVQWSUYLVRUKZRWSCRZUYAUVPVHUYSCWSCUYSDCOWHV
      AVBUYTUXIUVBUXTUVOUYTUXHUUFUWSUVAUYTUXGUUEUJUYTUXFUUDUUCWSCHWIWDVDUYTUWRU
      UTEFIIUYTUWQUUSGIUYTXQUURUUMUYTXNUUOXPUUQUYTXNXCUUNWSULUUOUYTXMUUNXCWSXEX
      IWSCWKWGXCUUNWSCWKWLUYTXPUUPXIWSULUUQUYTXOUUPXIWSXCXEWSCWKWEUUPXIWSCWKWLW
      MWFVPWBVLUYTUXNUVHUXSUVNUYTUXLUVGKEJIUYTUXKUVFUVCUYTUXJUVEXIUVDXIWSCWKVKW
      FWBUYTUXQUVMKEJIUYTUXPUVLUVJUYTUXOUVKXIXIUVDWSCWKVKWFWBVLVLVQWNVQ $.
      $( [24-Oct-2007] $)
  $}

  $( Lemma for ~ 0ded . $)
  0alg $p |- <. <. (/) , (/) >. , <. (/) , (/) >. >. e. Alg $=
    ( c0 cop calg wcel wf w3a wfun cdm cxp wss crn cvv wa wb 0ex 3pm3.2i eqcomi
    dm0 isalg mp2an f0 fun0 ssid xp0r 3sstr4i rn0 eqimssi mpbir2an ) AABZUIBCDZ
    AAAEZUKUKFZAGZAHZAAIZJZAKZAJZFZALDZUTUTFUTUJULUSMNUTUTUTOOOPOLLAAALLAAAUNAR
    QZVASTUKUKUKAUAZVBVBPUMUPURUBAAUNUOAUCRAUDUEUQAUFUGPUH $.
    $( [10-Jan-2008] $)


  ${
    $d a f g $.
    $( A deductive system with no object and no morphism.  (Contributed by FL,
       10-Jan-2008.) $)
    0ded $p |- <. <. (/) , (/) >. , <. (/) , (/) >. >. e. Ded $=
      ( va vg vf c0 cop wcel cv cfv wceq wa wral wb w3a cvv 0ex 3pm3.2i pm2.21i
      wi sylbi rgen cded calg cdm co eqid isded mp2an 0alg eleq2i noel mpbir2an
      dm0 pm3.2i ) DDEZUNEZUAFZUOUBFZAGZDHDHURIZUSJZADUCZKZBGZCGZEVAFVCDHZVDDHZ
      IZLBVAKZCVAKZMZVGVCVDDUDDHZVFIRBVAKZCVAKZVGVKVEIRBVAKZCVAKZJZDNFZVQVQMVQU
      PVJVPJLVQVQVQOOOPONNDDDCBNNDVAVAAVAUEZVRUFUGUQVBVIUHUTAVAURVAFURDFZUTVADU
      RULUIVSUTURUJQSTVHCVAVDVAFZVDDFZVHVADVDULUIZWAVHVDUJZQSTPVMVOVLCVAVTWAVLW
      BWAVLWCQSTVNCVAVTWAVNWBWAVNWCQSTUMUK $.
      $( [10-Jan-2008] $)
  $}

  ${
    $d a f g h $.
    $( Category ` 0 ` has no object and no morphism.  (Contributed by FL,
       10-Jan-2008.) $)
    0cat $p |- <. <. (/) , (/) >. , <. (/) , (/) >. >. e. Cat $=
      ( vh vg vf va c0 cop wcel cv cfv wceq wa co wi wral cvv 0ex pm2.21i sylbi
      dm0 rgen ccat cded cdm w3a wb 3pm3.2i eqid iscat mp2an 0ded eleq2i pm3.2i
      noel mpbir2an ) EEFZUOFZUAGZUPUBGZAHZEIBHZEIZJVACHZEIZJKUSUTVBELELUSUTELV
      BELJMAEUCZNBVDNZCVDNZKZVCDHZJZVHEIZVBELVBJMCVDNZDVDNZVIVBVJELVBJMCVDNZDVD
      NZKZEOGZVPVPUDVPUQVGVOKUEVPVPVPPPPUFPOOEEECBAOOEVDVDDVDUGZVQUHUIURVFUJVEC
      VDVBVDGVBEGZVEVDEVBSUKVRVEVBUMQRTULVLVNVKDVDVHVDGZVHEGZVKVDEVHSUKZVTVKVHU
      MZQRTVMDVDVSVTVMWAVTVMWBQRTULUN $.
      $( [10-Jan-2008] $)
  $}


  ${
    $d A u x y z $.
    1cat.1 $e |- A e. _V $.
    $( Category ` 1 ` has one object and one morphism.  (Contributed by FL,
       30-Oct-2007.) $)
    1cat $p |- <. <. { <. <. A , A >. , A >. } ,
     { <. <. A , A >. , A >. } >. , <. { <. A , <. A , A >. >. } ,
     { <. <. <. A , A >. , <. A , A >. >. , <. A , A >. >. } >. >.
     e. Cat $=
      ( vu vz vy vx cop csn wcel cv cfv wceq wa co cvv elsni id eqcomd opreq12d
      wral ccat cded wi w3a wb snex 3pm3.2i cdm dmsnop eqcomi iscat 1ded df-opr
      mp2an opex fvsn eqtri opreq12i 3ad2ant3 3ad2ant2 3ad2ant1 opreq1d opreq2d
      eqtr2i a1i 3eqtrd eqeq12d syl5ib syl3an rgen3 pm3.2i 3eqtri adantr fveq2d
      opreq1i adantl syl2an rgen2 opreq2i mpbir2an ) AAGZAGZHZWCGAWAGZHZWAWAGZW
      AGZHZGGZUAIZWIUBIZCJZWCKDJZWCKZLWNEJZWCKZLMZWLWMWOWHNZWHNZWLWMWHNZWOWHNZL
      ZUCZCWAHZTDXDTEXDTZMZWPFJZLZXGWEKZWOWHNZWOLZUCZEXDTFAHZTZXHWOXIWHNZWOLZUC
      ZEXDTFXMTZMZWCOIZXTWEOIZUDWHOIWJXFXSMUEXTXTYAWBUFZYBWDUFUGWGUFOOWCWCWHEDC
      OOWEXDXMFWCUHXDWAAUIUJWEUHXMAWAUIUJUKUNWKXEABULXCEDCXDXDXDWOXDIZWOWALZWMX
      DIWMWALZWLXDIWLWALZXCWOWAPZWMWAPWLWAPWQWAWAWAWHNZWHNZYHWAWHNZLZYDYEYFUDZX
      BYKWQWAYHYHWAWHYHWFWHKZWAWAWAWHUMZWFWAWAWAUOAAUOZUPZVDYHYMWAYNYPUQURVEYLY
      IWSYJXAYLWAWLYHWRWHYFYDWAWLLYEYFWLWAYFQRZUSYLWAWMWAWOWHYEYDWAWMLYFYEWMWAY
      EQRZUTYDYEWAWOLZYFYDWOWAYDQRZVASSYLYJWLWAWHNZWAWHNZWTWAWHNZXAYFYDYJUUBLYE
      YFYHUUAWAWHYFWAWLWAWHYQVBVBUSYEYDUUBUUCLYFYEUUAWTWAWHYEWAWMWLWHYRVCVBUTYD
      YEUUCXALYFYDWAWOWTWHYTVCVAVFVGVHVIVJVKXNXRXLFEXMXDXGXMIZXGALZYDXLYCXGAPZY
      GXHAWEKZWAWHNZWALZUUEYDMZXKUUIXHUUHYHYMWAUUGWAWAWHAWABYOUPZVOYNYPVLVEUUJU
      UHXJWAWOUUJUUGXIWAWOWHUUJAXGWEUUEAXGLYDUUEXGAUUEQRVMVNZYDYSUUEYTVPZSUUMVG
      VHVQVRXQFEXMXDUUDUUEYDXQYCUUFYGXHWAUUGWHNZWALZUUJXPUUOXHUUNYHYMWAUUGWAWAW
      HUUKVSYNYPVLVEUUJUUNXOWAWOUUJWAWOUUGXIWHUUMUULSUUMVGVHVQVRVKVT $.
      $( [30-Oct-2007] $)
  $}

  ${
    $d f g t u v w x y z $.
    $( Structure of a category.  (Contributed by FL, 26-Oct-2007.) $)
    strcat $p |- Cat C_ ( ( _V X. _V ) X. ( _V X. _V ) ) $=
      ( vx vg vf vv vu vw vz vy vt cv cop wceq cfv wa co wi cdm wral wex cvv
      ccat cded wcel cab cxp df-cat stcat eqsstri ) UAAJBJZCJZKDJZEJZKKZLUMUBUC
      FJZUIMGJZUJMLUOUIMHJZUJMZLNUNUOUPULOULOUNUOULOUPULOLPFUIQZRGURRHURRNUQIJZ
      LUSUKMZUPULOUPLPHURRIUKQZRUPUIMUSLUPUTULOUPLPHURRIVARNNZNESDSCSBSAUDTTUEZ
      VCUEAHGFDEICBUFVBABCEDUGUH $.
      $( [26-Oct-2007] $)
  $}

  $( A category is a relation.  (Contributed by FL, 26-Oct-2007.) $)
  relcat $p |- Rel Cat $=
    ( ccat wrel cvv cxp wss strcat xpss sstri df-rel mpbir ) ABACCDZEAKKDKFKKGH
    AIJ $.
    $( [26-Oct-2007] $)

  $( The domain of a category is a relation.  (Contributed by FL,
     26-Oct-2007.) $)
  reldcat $p |- Rel dom Cat $=
    ( ccat cdm wrel cvv cxp wss strcat dmss ax-mp dmxpid sseqtri df-rel mpbir )
    ABZCNDDEZFNOOEZBZOAPFNQFGAPHIOJKNLM $.
    $( [26-Oct-2007] $)

  $( The range of a category is a relation.  (Contributed by FL,
     26-Oct-2007.) $)
  relrcat $p |- Rel ran Cat $=
    ( ccat crn wrel cvv cxp wss strcat rnss ax-mp wne wceq vn0 pm3.2i xpnz mpbi
    c0 wa rnxp sseqtri df-rel mpbir ) ABZCUBDDEZFUBUCUCEZBZUCAUDFUBUEFGAUDHIUCP
    JZUEUCKDPJZUGQUFUGUGLLMDDNOUCUCRISUBTUA $.
    $( [26-Oct-2007] $)

  ${
    $d T w x y z $.
    $( A category is a deductive system.  (Contributed by FL, 26-Oct-2007.) $)
    catded $p |- ( T e. Cat -> T e. Ded ) $=
      ( vz vy vx vw ccat cfv cop c1st c2nd wrel wceq eqid opeq12i cv wa co wral
      cdm wi wcel cdom_ ccod_ cid_ co_ cded crn relcat reldcat relrcat 11st22nd
      w3a 3pm3.2i mpan domval codval idval cmpval syl6eqr cati simpld eqeltrd )
      AFUAZAAUBGZAUCGZHZAUDGZAUEGZHZHZUFVCAAIGZIGZVKJGZHZAJGZIGZVOJGZHZHZVJFKZF
      SKZFUGKZULVCAVSLVTWAWBUHUIUJUMAFUKUNVFVNVIVRVDVLVEVMVDAVDMZUOVEAVEMZUPNVG
      VPVHVQAVGVGMZUQAVHVHMZURNNUSVCVJUFUAZBOZVDGCOZVEGLWIVDGDOZVEGZLPWHWIWJVHQ
      VHQWHWIVHQWJVHQLTBVDSZRCWLRDWLRZVCWGWMPWKEOZLWNVGGZWJVHQWJLTDWLREVGSZRWJV
      DGWNLWJWOVHQWJLTDWLREWPRPVEVDVHADCBVGWLWPEWCWDWEWFWLMWPMUTVAVAVB $.
      $( [26-Oct-2007] $)
  $}

  ${
    cat.1 $e |- M = dom D $.
    cat.3 $e |- D = ( dom_ ` T ) $.
    ${
      cat.2 $e |- O = dom J $.
      cat.5 $e |- J = ( id_ ` T ) $.
      ${
        $( The 1st "axiom" of a category: ` ( dom_ `` T ) ` is a mapping from
           the morphisms of ` T ` to the objects of ` T ` .  (Contributed by
           FL, 2-Jan-2008.) $)
        domc $p |- ( T e. Cat -> D : M --> O ) $=
          ( ccat wcel cded calg wf catded dedalg doma 3syl ) BJKBLKBMKDEANBOBPA
          BCDEFGHIQR $.
          $( [2-Jan-2008] $)
      $}

      ${
        cat.4 $e |- C = ( cod_ ` T ) $.
        $( The 2nd "axiom" of a category ` ( cod_ `` T ) ` is a mapping from
           the morphisms of ` T ` to the objects of ` T ` .  (Contributed by
           FL, 2-Jan-2008.) $)
        codc $p |- ( T e. Cat -> C : M --> O ) $=
          ( ccat wcel cded calg wf catded dedalg coda 3syl ) CLMCNMCOMEFAPCQCRA
          BCDEFGHIJKST $.
          $( [2-Jan-2008] $)
      $}

      ${
        $( The 3rd "axiom" of a category ` ( id_ `` T ) ` is a mapping from the
           objects of ` T ` to the morphisms of ` T ` .  (Contributed by FL,
           5-Dec-2007.) $)
        idc $p |- ( T e. Cat -> J : O --> M ) $=
          ( ccat wcel cded calg wf catded dedalg ida 3syl ) BJKBLKBMKEDCNBOBPAB
          CDEFGHIQR $.
          $( [5-Dec-2007] $)
      $}
    $}

    ${
      cat.6 $e |- R = ( o_ ` T ) $.
      $( The 4th "axiom" of a category: ` ( o_ `` T ) ` is a partial operation
         from the morphisms of ` T ` to the morphisms of ` T ` .  (Contributed
         by FL, 10-Mar-2008.) $)
      cmppfc $p |- ( T e. Cat ->
        ( Fun R /\ dom R C_ ( M X. M ) /\ ran R C_ M ) ) $=
        ( ccat wcel cded calg wfun cdm cxp wss crn w3a catded dedalg cmppfa
        3syl ) CHICJICKIBLBMDDNOBPDOQCRCSABCDEFGTUA $.
        $( [10-Mar-2008] $)
    $}
  $}

  ${
    idosc.1 $e |- O = dom J $.
    idosc.2 $e |- D = ( dom_ ` T ) $.
    idosc.3 $e |- J = ( id_ ` T ) $.
    idosc.4 $e |- C = ( cod_ ` T ) $.
    $( The 5th "axiom" of a category: identities are morphisms whose domains
       and codomains are equal.  (Contributed by FL, 5-Dec-2007.) $)
    idosc $p |- ( ( T e. Cat /\ A e. O ) -> ( ( D ` ( J ` A ) ) = A /\
        ( C ` ( J ` A ) ) = A ) ) $=
      ( ccat wcel cded cfv wceq wa catded idosd sylan ) DKLDMLAFLAENZCNAOTBNAOP
      DQABCDEFGHIJRS $.
      $( [5-Dec-2007] $)
  $}

  ${
    cmppfcd.1 $e |- M = dom D $.
    cmppfcd.2 $e |- D = ( dom_ ` T ) $.
    cmppfcd.3 $e |- C = ( cod_ ` T ) $.
    cmppfcd.4 $e |- R = ( o_ ` T ) $.
    $( The 6th "axiom" of a category: ` ( G ( o_ `` T ) F ) ` is only defined
       when the domain of ` F ` equals the codomain of ` G ` .  (Contributed by
       FL, 10-Mar-2008.) $)
    cmppfcd $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
           ( <. G , F >. e. dom R <-> ( D ` G ) = ( C ` F ) ) ) $=
      ( ccat wcel cded cop cdm cfv wceq wb catded cmppfd syl3an1 ) DLMDNMEGMFGM
      FEOCPMFBQEAQRSDTABCDEFGHIJKUAUB $.
      $( [10-Mar-2008] $)


    $( The 7th "axiom" of a category: when ` ( G ( o_ `` T ) F ) ` is defined
       its domain is the domain of ` F ` .  (Contributed by FL,
       10-Mar-2008.) $)
    domcmpc $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
        ( ( D ` G ) = ( C ` F ) -> ( D ` ( G R F ) ) = ( D ` F ) ) ) $=
      ( ccat wcel cded cfv wceq co wi catded domcmpd syl3an1 ) DLMDNMEGMFGMFBOE
      AOPFECQBOEBOPRDSABCDEFGHIJKTUA $.
      $( [10-Mar-2008] $)


    $( The 8th "axiom" of a category: when ` ( G ( o_ `` T ) F ) ` is defined
       its codomain is the codomain of ` G ` .  (Contributed by FL,
       10-Jan-2008.) $)
    codcmpc $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
              ( ( D ` G ) = ( C ` F )
              -> ( C ` ( G R F ) ) = ( C ` G ) ) ) $=
      ( ccat wcel cded cfv wceq co wi catded codcmpd syl3an1 ) DLMDNMEGMFGMFBOE
      AOPFECQAOFAOPRDSABCDEFGHIJKTUA $.
      $( [10-Jan-2008] $)
  $}


  ${
    $d C f g h x $.  $d D f g h x $.  $d F f g h $.  $d G g h $.  $d H h $.
    $d M f g h $.  $d R f g h x $.  $d T f x $.
    cmpasso.1 $e |- M = dom D $.
    cmpasso.2 $e |- D = ( dom_ ` T ) $.
    cmpasso.5 $e |- C = ( cod_ ` T ) $.
    cmpasso.6 $e |- R = ( o_ ` T ) $.
    $( The 9th "axiom" of a category: ` ( o_ `` T ) ` is associative.
       (Contributed by FL, 29-Oct-2007.) $)
    cmpasso $p |- ( ( T e. Cat /\ ( F e. M /\ G e. M /\ H e. M ) ) ->
              (
                ( ( D ` H ) = ( C ` G ) /\
                ( D ` G ) = ( C ` F ) )
              ->
                ( H R ( G R F ) ) =
                  ( ( H R G ) R F )
               ) ) $=
      ( vf wcel cfv wceq wa co wi wral vh vg vx ccat w3a cop cid_ cded cdm eqid
      cv cati fveq2 eqeq2d anbi2d opreq2 opreq2d eqeq12d imbi12d eqeq1d anbi12d
      opreq1 opreq1d anbi1d rcla43v com12 ad2antlr syl imp ) DUDNZEHNFHNGHNUEZG
      BOZFAOZPZFBOZEAOZPZQZGFECRZCRZGFCRZECRZPZSZVJBAUFDUGOZCUFUFUHNZUAUKZBOZUB
      UKZAOZPZWIBOZMUKZAOZPZQZWGWIWMCRZCRZWGWICRZWMCRZPZSZUAHTUBHTMHTZQWNUCUKZP
      XDWEOZWMCRWMPSMHTUCWEUIZTWMBOXDPWMXECRWMPSMHTUCXFTQZQVKWDSZABCDMUBUAWEHXF
      UCJKWEUJLIXFUJULXCXHWFXGVKXCWDXBWDWKWLVPPZQZWGWIECRZCRZWSECRZPZSWHVMPZVQQ
      ZWGVSCRZWGFCRZECRZPZSMUBUAEFGHHHWMEPZWPXJXAXNYAWOXIWKYAWNVPWLWMEAUMUNUOYA
      WRXLWTXMYAWQXKWGCWMEWICUPUQWMEWSCUPURUSWIFPZXJXPXNXTYBWKXOXIVQYBWJVMWHWIF
      AUMUNYBWLVOVPWIFBUMUTVAYBXLXQXMXSYBXKVSWGCWIFECVBUQYBWSXRECWIFWGCUPVCURUS
      WGGPZXPVRXTWCYCXOVNVQYCWHVLVMWGGBUMUTVDYCXQVTXSWBWGGVSCVBYCXRWAECWGGFCVBV
      CURUSVEVFVGVHVI $.
      $( [29-Oct-2007] $)
  $}

  ${
    $d A a f $.  $d C a f x y $.  $d D a f x y $.  $d F f $.  $d J a f $.
    $d M a f $.  $d O a $.  $d R a f x y $.
    cmpida.1 $e |- M = dom D $.
    cmpida.2 $e |- D = ( dom_ ` T ) $.
    cmpida.3 $e |- O = dom J $.
    cmpida.4 $e |- J = ( id_ ` T ) $.
    cmpida.5 $e |- R = ( o_ ` T ) $.
    cmpida.6 $e |- C = ( cod_ ` T ) $.
    $( The 10th "axiom" of a category: ` ( J `` A ) ` is a left neutral
       element.  (Contributed by FL, 29-Oct-2007.) $)
    cmpida $p |- ( ( T e. Cat /\ A e. O /\ F e. M ) -> ( ( C ` F ) = A
         -> ( ( J ` A ) R F ) = F ) ) $=
      ( vf cfv wceq co wral vy vx va ccat wcel wi cded cv wa cati eqeq2 opreq1d
      cop fveq2 eqeq1d imbi12d opreq2 eqeq12d rcla42v com12 ad2antrl syl 3impib
      id ) EUDUEZAIUEZFHUEZFBQZARZAGQZFDSZFRZUFZVECBUMGDUMUMUGUEUAUHZCQUBUHZBQR
      VOCQPUHZBQZRUIVNVOVPDSDSVNVODSVPDSRUFUAHTUBHTPHTUIZVQUCUHZRZVSGQZVPDSZVPR
      ZUFZPHTUCITZVPCQVSRVPWADSVPRUFPHTUCITZUIUIVFVGUIZVMUFZBCDEPUBUAGHIUCKOMNJ
      LUJWEWHVRWFWGWEVMWDVMVQARZVJVPDSZVPRZUFUCPAFIHVSARZVTWIWCWKVSAVQUKWLWBWJV
      PWLWAVJVPDVSAGUNULUOUPVPFRZWIVIWKVLWMVQVHAVPFBUNUOWMWJVKVPFVPFVJDUQWMVDUR
      UPUSUTVAVBVC $.
      $( [29-Oct-2007] $)
  $}

  ${
    $d A a f $.  $d D a f x y $.  $d F f $.  $d J a f $.  $d M a f $.
    $d O a $.  $d R a f x y $.  $d T a f x y $.
    cmpidb.1 $e |- M = dom D $.
    cmpidb.2 $e |- D = ( dom_ ` T ) $.
    cmpidb.3 $e |- O = dom J $.
    cmpidb.4 $e |- J = ( id_ ` T ) $.
    cmpidb.5 $e |- R = ( o_ ` T ) $.
    $( The 11th "axiom" of a category: ` ( J `` A ) ` is a right neutral
       element.  (Contributed by FL, 24-Oct-2007.) $)
    cmpidb $p |- ( ( T e. Cat /\ A e. O /\ F e. M ) -> ( ( D ` F ) = A
         -> ( F R ( J ` A ) ) = F ) ) $=
      ( vf va cfv wceq co wi wral vy vx ccat wcel ccod_ cded cv eqid cati eqeq2
      cop wa fveq2 opreq2d eqeq1d imbi12d opreq1 eqeq12d rcla42v com12 ad2antll
      id syl 3impib ) DUCUDZAHUDZEGUDZEBPZAQZEAFPZCRZEQZSZVEBDUEPZUKFCUKUKUFUDU
      AUGZBPUBUGZVNPQVPBPNUGZVNPZQULVOVPVQCRCRVOVPCRVQCRQSUAGTUBGTNGTULZVROUGZQ
      VTFPZVQCRVQQSNGTOHTZVQBPZVTQZVQWACRZVQQZSZNGTOHTZULULVFVGULZVMSZVNBCDNUBU
      AFGHOJVNUHLMIKUIWHWJVSWBWIWHVMWGVMWCAQZVQVJCRZVQQZSONAEHGVTAQZWDWKWFWMVTA
      WCUJWNWEWLVQWNWAVJVQCVTAFUMUNUOUPVQEQZWKVIWMVLWOWCVHAVQEBUMUOWOWLVKVQEVQE
      VJCUQWOVBURUPUSUTVAVCVD $.
      $( [24-Oct-2007] $)
  $}

  ${
    dmo.1 $e |- M = dom ( dom_ ` T ) $.
    dmo.3 $e |- O = dom ( id_ ` T ) $.

    ${
      dmo.2 $e |- D = ( dom_ ` T ) $.
      $( The domain of a morphism is an object.  (Contributed by FL,
         2-Jan-2008.) $)
      dmo $p |- ( ( T e. Cat /\ F e. M ) -> ( D ` F ) e. O ) $=
        ( ccat wcel wf cfv cid_ cdom_ cdm eqcomi dmeqi eqtri eqid domc ffvelrn
        sylan ) BIJDEAKCDJCALEJABBMLZDEDBNLZOAOFUDAAUDHPQRHGUCSTDECAUAUB $.
        $( [2-Jan-2008] $)

    $}

    ${
      cdmo.2 $e |- C = ( cod_ ` T ) $.
      $( The codomain of a morphism is an object.  (Contributed by FL,
         2-Jan-2008.) $)
      cdmo $p |- ( ( T e. Cat /\ F e. M ) -> ( C ` F ) e. O ) $=
        ( ccat wcel wf cfv cdom_ cid_ eqid codc ffvelrn sylan ) BIJDEAKCDJCALEJ
        ABMLZBBNLZDEFSOGTOHPDECAQR $.
        $( [2-Jan-2008] $)
    $}

    ${
      jdmo.2 $e |- J = ( id_ ` T ) $.
      $( An identity is a morphism.  (Contributed by FL, 10-Jan-2008.) $)
      jdmo $p |- ( ( T e. Cat /\ A e. O ) -> ( J ` A ) e. M ) $=
        ( ccat wcel wf cfv cdom_ eqid cid_ cdm dmeqi eqtr4i idc ffvelrn sylan )
        BIJEDCKAEJACLDJBMLZBCDEFUBNEBOLZPCPGCUCHQRHSEDACTUA $.
        $( [10-Jan-2008] $)
    $}

  $}

  ${
    cmpmorp.1 $e |- M = dom ( dom_ ` T ) $.
    cmpmorp.2 $e |- D = ( dom_ ` T ) $.
    cmpmorp.3 $e |- C = ( cod_ ` T ) $.
    cmpmorp.4 $e |- R = ( o_ ` T ) $.
    $( Conditions for a composite to be a morphism.  (Contributed by FL,
       10-Mar-2008.) $)
    cmpmorp $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
      ( ( D ` G ) = ( C ` F ) -> ( G R F ) e. M ) ) $=
      ( ccat wcel w3a cfv wceq co cdm wa wss crn wfun cop cxp cdom_ eqid cmppfc
      3ad2ant1 simp1d adantr eqcomi dmeqi cmppfcd biimpar jca ex fnoprvrn2 syl6
      eqtri wi simp3 ssel 3syl syld ) DLMZEGMZFGMZNZFBOEAOPZFECQZCUAZMZVJGMZVHV
      ICUBZFEUCCRZMZSZVLVHVIVQVHVISVNVPVHVNVIVHVNVOGGUDTZVKGTZVEVFVNVRVSNZVGDUE
      OZCDGHWAUFKUGUHZUIUJVHVPVIABCDEFGGWARBRHWABBWAIUKULUSIJKUMUNUOUPFECUQURVH
      VTVSVLVMUTWBVNVRVSVAVKGVJVBVCVD $.
      $( [10-Mar-2008] $)
  $}

  $( Two ways to define the set of the morphisms of a category.  (Contributed
     by FL, 19-Sep-2009.) $)
  morcat $p |- ( T e. Cat -> dom ( dom_ ` T ) = dom ( cod_ ` T ) ) $=
    ( ccat wcel cdom_ cfv cdm cid_ ccod_ wf wceq eqid codc fdm eqcomd syl ) ABC
    ADEZFZAGEZFZAHEZIZQTFZJTPARQSQKPKSKRKTKLUAUBQQSTMNO $.
    $( [19-Sep-2009] $)

  ${
    cmppfc1.1 $e |- R = ( o_ ` T ) $.
    $( Composition is a function.  (Contributed by FL, 5-Oct-2009.) $)
    cmppfc1 $p |- ( T e. Cat -> Fun R ) $=
      ( ccat wcel wfun cdm cdom_ cfv cxp wss crn eqid cmppfc simp1d ) BDEAFAGBH
      IZGZQJKALQKPABQQMPMCNO $.
      $( [5-Oct-2009] $)

  $}

  ${
    $d D x y z $.  $d F x y z $.  $d G x y z $.  $d R x y z $.  $d T x y z $.
    dualcat2lem.1 $e |- D = ( dom_ ` T ) $.
    dualcat2lem.2 $e |- C = ( cod_ ` T ) $.
    dualcat2lem.3 $e |- R = ( o_ ` T ) $.
    $( Lemma for ~ dualcat2 . $)
    dualcat2lem $p |- (
       ( T e. Cat /\ ( F e. dom D /\ G e. dom D ) /\ ( D ` G ) = ( C ` F ) )
            -> ( F { <. <. x , y >. , z >.
            | ( <. y , x >. e. dom R /\ z = ( y R x ) ) } G )
            = ( G R F ) ) $=
      ( wcel cdm wa wceq co copab2 wi cvv ccat cfv cv cop cxp wss wfun crn eqid
      cmppfc simp2d sseld anim1d ccnv cnvxp eqcomi eleq2i visset opelcnv anbi1i
      ssoprab2g bitri oprabbii twsvbdop eqtr sseq2 w3a wfn oprex fnoprab2 fnfun
      ax-mp a1i simpl3 cmppfcd biimprd 3expib 3imp1 wb elisset anim12i 3ad2ant2
      a1dd adantr opelcnvg mpbird bicomi dmeqi relcnv dmoprabss6 syl5eq 3adant3
      syl wrel eleq2d oprssoprvg eqcomd syl111anc simpr opreq2 opreq1 oprabval2
      ad2antlr eqtrd exp31 3imp mpd 3exp1 com3r syl6bi mp2an mpcom ) GUAMZHENZM
      ZIXNMZOZIEUBHDUBPZHIBUCZAUCZUDZFNZMZCUCXSXTFQZPZOZABCRZQZIHFQZPZYGYAXNXNU
      EZMZYEOZABCRZUFZXMXQXRYJSZSZXMYFYMABCXMYCYLYEXMYBYKYAXMFUGYBYKUFFUHXNUFEF
      GXNXNUIZJLUJUKULUMVAYNXTXSUDZYKMZYEOZABCRZPZUUBXTXNMXSXNMOYEOABCRZPZYOXMY
      QSZSZYMUUAABCYLYTYEYLYAYKUNZMYTYKUUHYAUUHYKXNXNUOUPUQXSXTYKBURZAURZUSVBUT
      VCYEABCXNXNVDUUCUUEOYNUUDPZUUGYNUUBUUDVEUUKYOYGUUDUFZUUFYNUUDYGVFXMXQUULY
      PXMXQUULXRYJXMXQUULVGZXROZYHHIUUDQZPZYJUUNUUDUGZUULHIUDZYGNZMZUUPUUQUUNUU
      DYKVHUUQABCXNXNYDUUDXSXTFVIZUUDUIZVJYKUUDVKVLVMXMXQUULXRVNUUNUUTUURYBUNZM
      ZUUNUVDIHUDYBMZXMXQUULXRUVEXMXQXRUVESZUULXMXOXPUVFXMXOXPVGUVEXRDEFGHIXNYR
      JKLVOVPVQWCVRUUNHTMZITMZOZUVDUVEVSUUMUVIXRXQXMUVIUULXOUVGXPUVHHXNVTIXNVTW
      AWBWDHITTYBWEWMWFUUMUUTUVDVSZXRXMXQUVJUULXMXQOZUUSUVCUURUVKUUSYSUVCMZYEOZ
      ABCRZNZUVCYGUVNYFUVMABCYCUVLYEUVLYCXTXSYBUUJUUIUSWGUTVCWHUVKUVCWNZUVOUVCP
      UVPUVKYBWIVMABCUVCYDTUVAWJWMWKWOWLWDWFUUQUULUUTVGUUOYHHIUUDYGWPWQWRUUMUUP
      YJSZXRXMXQUULUVQXMXQUVQUULXMXQUUPYJUVKUUPOYHUUOYIUVKUUPWSXQUUOYIPXMUUPABC
      HIXNXNYDYIUUDXSHFQIHFVIXTHXSFWTXSIHFXAUVBXBXCXDXEWCXFWDXGXHXIXJWMXKXLXF
      $.
      $( [18-Apr-2010] $) $( [27-Mar-2010] $)
  $}

  ${
    $d D x y z $.  $d F x y z $.  $d G x y z $.  $d R x y z $.  $d T x y z $.
    dualded2lem.1 $e |- D = ( dom_ ` T ) $.
    dualded2lem.2 $e |- C = ( cod_ ` T ) $.
    dualded2lem.3 $e |- R = ( o_ ` T ) $.
    $( Lemma for ~ dualded . $)
    dualded2lem $p |- (
       ( T e. Ded /\ ( F e. dom D /\ G e. dom D ) /\ ( D ` G ) = ( C ` F ) )
          -> ( F { <. <. x , y >. , z >.
          | ( <. y , x >. e. dom R /\ z = ( y R x ) ) } G ) = ( G R F ) ) $=
      ( wcel cdm wa wceq cop co copab2 cvv cded cfv cv cxp wss wi wfun crn calg
      dedalg eqid cmppfa simp2d sseld anim1d ssoprab2g ccnv cnvxp eqcomi eleq2i
      w3a syl visset opelcnv bitri anbi1i oprabbii twsvbdop eqtr sseq2 fnoprab2
      wfn oprex fnfun ax-mp a1i simpl3 wb 3expb 3adant3 biimpar elisset anim12i
      cmppfd 3ad2ant2 adantr opelcnvg mpbird bicomi wrel relcnv syl5eq eleqtrrd
      dmeqi dmoprabss6 oprssoprvg eqcomd syl111anc simpl2l simpl2r opreq2 eqtrd
      opreq1 oprabval2g 3exp1 com3r syl6bi mp2an mpcom 3imp ) GUAMZHENZMZIXLMZO
      ZIEUBHDUBPZHIBUCZAUCZQZFNZMZCUCXQXRFRZPZOZABCSZRZIHFRZPZYEXSXLXLUDZMZYCOZ
      ABCSZUEZXKXOXPYHUFZUFZXKYDYKABCXKYAYJYCXKXTYIXSXKFUGZXTYIUEZFUHXLUEZXKGUI
      MYPYQYRVAGUJEFGXLXLUKZJLULVBUMUNUOUPYLXRXQQZYIMZYCOZABCSZPZUUCXRXLMXQXLMO
      YCOABCSZPZYMXKYOUFZUFZYKUUBABCYJUUAYCYJXSYIUQZMUUAYIUUIXSUUIYIXLXLURUSUTX
      QXRYIBVCZAVCZVDVEVFVGYCABCXLXLVHUUDUUFOYLUUEPZUUHYLUUCUUEVIUULYMYEUUEUEZU
      UGYLUUEYEVJXKXOUUMYNXKXOUUMXPYHXKXOUUMVAZXPOZYFHIUUERZYGUUOUUEUGZUUMHIQZY
      ENZMZYFUUPPUUQUUOUUEYIVLUUQABCXLXLYBUUEXQXRFVMZUUEUKZVKYIUUEVNVOVPXKXOUUM
      XPVQUUOUURXTUQZUUSUUOUURUVCMZIHQXTMZUUNUVEXPXKXOUVEXPVRZUUMXKXMXNUVFDEFGH
      IXLYSJKLWDVSVTWAUUOHTMZITMZOZUVDUVEVRUUNUVIXPXOXKUVIUUMXMUVGXNUVHHXLWBIXL
      WBWCWEWFHITTXTWGVBWHUUNUUSUVCPZXPXKXOUVJUUMXKXOOZUUSYTUVCMZYCOZABCSZNZUVC
      YEUVNYDUVMABCYAUVLYCUVLYAXRXQXTUUKUUJVDWIVFVGWNUVKUVCWJZUVOUVCPUVPUVKXTWK
      VPABCUVCYBTUVAWOVBWLVTWFWMUUQUUMUUTVAUUPYFHIUUEYEWPWQWRUUOXMXNYGTMZUUPYGP
      XMXNXKUUMXPWSXMXNXKUUMXPWTUVQUUOIHFVMVPABCHIXLXLYBYGUUEXQHFRTXRHXQFXAXQIH
      FXCUVBXDWRXBXEXFXGVBXHXIXJ $.
      $( [18-Apr-2010] $)
  $}

  ${
    dualcat2.1 $e |- D = ( dom_ ` T ) $.
    dualcat2.2 $e |- C = ( cod_ ` T ) $.
    dualcat2.3 $e |- J = ( id_ ` T ) $.
    dualcat2.4 $e |- R = ( o_ ` T ) $.
    ${
      $d C w x y z $.  $d R w x y z $.  $d T x y z $.
      $( The dual of a ` Alg ` is a ` Alg ` .  (Contributed by FL,
         18-Apr-2010.) $)
      dualalg $p |- ( T e. Alg ->
        <. <. C , D >. , <. J , { <. <. x , y >. , z >. |
          ( <. y , x >. e. dom R /\ z = ( y R x ) ) } >. >. e. Alg ) $=
        ( wcel cop wceq wa cvv w3a wss wi vw calg cv co copab2 ccnv wb opelcnvg
        cdm visset bicomd mp2an a1i anbi1d oprabbidv opeq2d wfun cxp dcsda eqid
        crn algi feq2 biimpd feq3 3anim123d idd xpeq12 anidms sseq2 syl anim12d
        simp2 simp1 simp3 3jca cnvss sqpsym sstr2 ssel syl6 mpi 3ad2ant2 anim1d
        wf ssoprab2g oprex twsvbdop fnoprab2 fnfun ax-mp funss sylc wrel relcnv
        wfn dmoprabss6 cnvxp sseq1 eqcoms com12 syl6bi mpsyl syl5com cuni copab
        csn df-opr eqeq2i opswap eqcomi fveq2i sneq unieqd fveq2d syl5eq eqeq2d
        cfv cnveq syl5bb dfoprab4spop rneqi wex elrel opelcnv eleq1 imp 3adant1
        wral fvelrn syl11anc 3exp com14 imbi2d ccod_ fvex eqeltri cdom_ cid_
        co_ a1d sylbi eleq1d 3imtr4d 19.23aivv ex pm2.43d 3imp r19.21aiv fopab2
        com25 sylib frn sstrd syl5eqss anim12i 3pm3.2i dmex oprabex2gpop pm3.2i
        cnvex isalg mpbird eqeltrd ) GUBMZDENZHBUCZAUCZNZFUIZMZCUCZUVGUVHFUDZOZ
        PZABCUEZNZNUVFHUVHUVGNZUVJUFZMZUVNPZABCUEZNZNZUBUVEUVQUWCUVFUVEUVPUWBHU
        VEUVOUWAABCUVEUVKUVTUVNUVKUVTUGZUVEUVHQMZUVGQMZUWEAUJZBUJZUWFUWGPUVTUVK
        UVHUVGQQUVJUHUKULUMUNUOUPUPUVEUWDUBMZDUIZHUIZDWEZUWKUWLEWEZUWLUWKHWEZRZ
        UWBUQZUWBUIZUWKUWKURZSZUWBVAZUWKSZRZPZUVEEUIZUWKOZUXEUWLEWEZUXEUWLDWEZU
        WLUXEHWEZRZFUQZUVJUXEUXEURZSZFVAZUXESZRZPZUXDDEGIJUSDEFGHUXEUWLIJKLUXEU
        TUWLUTZVBUXFUXQUWNUWMUWORZUXKUVJUWSSZUXNUWKSZRZPUXDUXFUXJUXSUXPUYBUXFUX
        GUWNUXHUWMUXIUWOUXFUXGUWNUXEUWKUWLEVCVDUXFUXHUWMUXEUWKUWLDVCVDUXFUXIUWO
        UXEUWKUWLHVEVDVFUXFUXKUXKUXMUXTUXOUYAUXFUXKVGUXFUXLUWSOZUXMUXTTUXFUYCUX
        EUWKUXEUWKVHVIUYCUXMUXTUXLUWSUVJVJVDVKUXFUXOUYAUXEUWKUXNVJVDVFVLUXSUWPU
        YBUXCUXSUWMUWNUWOUWNUWMUWOVMUWNUWMUWOVNUWNUWMUWOVOVPUYBUWQUWTUXBUYBUWBU
        VRUWSMZUVNPZABCUEZSUYFUQZUWQUYBUWAUYEABCUYBUVTUYDUVNUXTUXKUVTUYDTZUYAUX
        TUVSUWSUFZSZUYHUVJUWSVQZUYJUYIUWSSZUYHUWKVRUYJUYLUVSUWSSZUYHUVSUYIUWSVS
        UVSUWSUVRVTWAWBVKWCWDWFUYGUYBUYFUWSWPUYGABCUWKUWKUVMUYFUVGUVHFWGZUVNABC
        UWKUWKWHWIUWSUYFWJWKUMUWBUYFWLWMUVSWNZUYBUWTTUVJWOZUYOUWRUVSOZUYBUWTABC
        UVSUVMQUYNWQUXTUXKUYQUWTTZUYAUYIUWSOZUXTUYJUYRUWKUWKWRUYKUYSUYJUYMUYRUY
        IUWSUVSVJUYQUYMUWTUYMUWTTUVSUWRUVSUWROUYMUWTUVSUWRUWSWSVDWTXAXBXCWCXDWK
        UYBUXAUAUCZUVSMZUVLUYTXGZUFZXEZFXRZOZPUACXFZVAZUWKUWBVUGUYOUWBVUGOUYPUV
        NVUFABCUAUVSUVNUVLUVIFXRZOUVRUYTOZVUFUVMVUIUVLUVGUVHFXHXIVUJVUIVUEUVLVU
        JVUIUVRXGZUFZXEZFXRZVUEUVIVUMFVUMUVIUVHUVGUWHUWIXJZXKXLVUJVUMVUDFVUJVUL
        VUCVUJVUKVUBOVULVUCOUVRUYTXMVUKVUBXSVKXNXOXPXQXTYAWKYBUYBVUHUXNUWKUYBUV
        SUXNVUGWEZVUHUXNSUYBVUEUXNMZUAUVSYIVUPUYBVUQUAUVSUXKUXTUYAVUAVUQTZUXTUX
        KUYAVURTZUYOUXTUXKVUSTTUYPUYOVUAUXKUYAUXTVUQUYOVUAUXKUYAUXTVUQTZTZTZUYO
        VUAVUAVVBTZUYOVUAPUYTUVROZBYCAYCVVCABUYTUVSYDVVDVVCABVVDUVTUXKUYAUXTVUN
        UXNMZTZTZTZVUAVVBUVTVVDVVHUVTUVKVVDVVHTUVHUVGUVJUWHUWIYEUVKVVHVVDVUMUVI
        OZUVKVVHTVUOUYAUVKUXKVVIVVFUVKUXKVVIVVFTTTUYAUXTUXKVVIUVKVVEUXKVVIUVKVV
        ETTTUXTUXKVVIUVKVVEUXKVVIUVKRUXKVUMUVJMZVVEUXKVVIUVKVNVVIUVKVVJUXKVVIUV
        KVVJUVKVVJTUVIVUMUVIVUMOUVKVVJUVIVUMUVJYFVDWTYGYHVUMFYJYKYLUMYMUMYMWKUU
        AUUBXAUYTUVRUVSYFVVDVVAVVGUXKVVDVUTVVFUYAVVDVUQVVEUXTVVDVUEVUNUXNVVDVUD
        VUMFVVDVUCVULVVDVUBVUKOVUCVULOUYTUVRXMVUBVUKXSVKXNXOUUCYNYNYNUUDUUEVKUU
        FUUGUUKWKXAUUHUUIUACUVSUXNVUEVUGVUGUTUUJUULUVSUXNVUGUUMVKUXKUXTUYAVOUUN
        UUOVPUUPWAWMUVEDQMZEQMZHQMZRZUWBQMZPZUWJUXDUGVVPUVEVVNVVOVVKVVLVVMDGYOX
        RQJGYOYPYQEGYRXRQIGYRYPYQHGYSXRQKGYSYPYQUUQUVSQMUYOVVOUVJFFGYTXRQLGYTYP
        YQUURUVAUYPABCUVMQUVSUUSULUUTUMQQEDUWBQQHUWKUWLUWKUTUXRUVBVKUVCUVD $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d C a f g $.  $d C f g x y z $.  $d D a f g $.  $d D f g x y z $.
      $d J a $.  $d R f g x y z $.  $d T a f g $.  $d T f g x y z $.
      $( The dual of a deductive system is a deductive system.  (Contributed by
         FL, 18-Apr-2010.) $)
      dualded $p |- ( T e. Ded ->
        <. <. C , D >. , <. J , { <. <. x , y >. , z >. |
        ( <. y , x >. e. dom R /\ z = ( y R x ) ) } >. >. e. Ded ) $=
        ( vg vf wcel wceq wa cfv wi cvv va cded cop cv cdm copab2 calg wral w3a
        co wb dedalg dualalg eqid idosd ancomd r19.21aiva visset opelcnv bicomi
        syl ccnv anbi1i oprabbii dmeqi relcnv oprex dmoprabss6 ax-mp a1i syl5eq
        wrel eleq2d dcsda cmppfd 3expib eleq2 anbi12d imbi1d syl5ibr 3syl com12
        eqcoms pm2.43i ancoms impcom eqcom 3bitrd r19.21aivv 3jca simpll eqcomd
        bitrd ex biimpa adantr biimpi adantl dualded2lem syl111anc codcmpd 3exp
        fveq2d imbi12d imbi2d mpbiri com3l eqtrd exp31 domcmpd jca32 ccod_ cid_
        id cdom_ eleq1i 3anbi123i fvex mpbir3an cxp wss crn cmppfa simp2d sseld
        wfun anim1d ssoprab2g cnvxp eleq2i eqeltri dmex xpex relxp oprabex2gpop
        bitr3i mp2an ssexg sylancl isded sylancr mpbird ) GUBOZDEUCHBUDZAUDZUCZ
        FUEZOZCUDUUDUUEFUJZPZQZABCUFZUCUCZUBOZUUMUGOZUAUDZHRZDRUUPPZUUQERUUPPZQ
        ZUAHUEZUHZMUDZNUDZUCZUULUEZOZUVCDRZUVDERZPZUKZMDUEZUHNUVLUHZUIZUVJUVCUV
        DUULUJZDRZUVDDRZPZSZMUVLUHNUVLUHZUVJUVOERZUVCERZPZSZMUVLUHNUVLUHZQQZUUC
        UVNUVTUWEUUCUUOUVBUVMUUCGUGOZUUOGULZABCDEFGHIJKLUMVAUUCUUTUAUVAUUCUUPUV
        AOQUUSUURUUPDEGHUVAUVAUNZIKJUOUPUQUUCUVKNMUVLUVLUUCUVDUVLOZUVCUVLOZQZUV
        KUUCUWLQZUVGUVEUUGVBZOZUVJUWMUVFUWNUVEUWMUVFUUEUUDUCZUWNOZUUJQZABCUFZUE
        ZUWNUULUWSUUKUWRABCUUHUWQUUJUWQUUHUUEUUDUUGAURZBURZUSUTVCVDVEUWTUWNPZUW
        MUWNVLUXCUUGVFABCUWNUUITUUDUUEFVGVHVIVJVKVMUWMUWOUVDUVCUCUUGOZUVIUVHPZU
        VJUWOUXDUKUWMUVCUVDUUGMURNURUSVJUWLUUCUXDUXEUKZUWKUWJUUCUXFSUUCUWKUWJQZ
        UXFUUCUXGUXFSZUUCUWGEUEZUVLPZUUCUXHSZUWHDEGIJVNZUXKUVLUXIUUCUXHUVLUXIPZ
        UVCUXIOZUVDUXIOZQZUXFSUUCUXNUXOUXFDEFGUVCUVDUXIUXIUNZIJLVOVPUXMUXGUXPUX
        FUXMUWKUXNUWJUXOUVLUXIUVCVQZUVLUXIUVDVQZVRVSVTWCWAWDWBWEWFUXEUVJUKUWMUV
        IUVHWGVJWHWMWNWIWJUUCUVSNMUVLUVLUUCUWLUVJUVRUWMUVJQZUVPUVDUVCFUJZDRZUVQ
        UXTUUCUXPUXEUVPUYBPUUCUWLUVJWKZUWMUXPUVJUWMUXOUXNUUCUWLUXOUXNQZUUCUWGUW
        LUYDUKUWHUWGUWJUXOUWKUXNUWGUVLUXIUVDUWGUXIUVLUXLWLZVMUWGUVLUXIUVCUYEVMV
        RVAWOUPWPZUVJUXEUWMUVJUXEUVHUVIWGWQWRZUUCUXPUXEUIZUVOUYADABCDEFGUVCUVDI
        JLWSZXCWTUVJUWMUYBUVQPZUWMUYJSUVIUVHUWMUXEUYJUWLUUCUXEUYJSZUWKUWJUUCUYK
        SUUCUWKUWJUYKUUCUWKUWJUYKSZSZUUCUWGUXJUUCUYMSZUWHUXLUXJUYNUUCUXNUXOUYKS
        ZSZSZUUCUXNUXOUYKDEFGUVCUVDUXIUXQIJLXAXBUYNUYQUKUVLUXIUXMUYMUYPUUCUXMUW
        KUXNUYLUYOUXRUXMUWJUXOUYKUXSVSXDXEWCXFWAWDXGWFWFWBWCWFXHXIWIUUCUWDNMUVL
        UVLUUCUWLUVJUWCUXTUWAUYAERZUWBUXTUUCUXPUXEUWAUYRPUYCUYFUYGUYHUVOUYAEUYI
        XCWTUVJUWMUYRUWBPZUWMUYSSUVIUVHUWMUXEUYSUWLUUCUXEUYSSZUWKUWJUUCUYTSUUCU
        WKUWJUYTUUCUWKUWJUYTSZSZUUCUWGUXJUUCVUBSUWHUXLUUCVUBUXJUXNUXOUYTSZSUUCU
        XNUXOUYTDEFGUVCUVDUXIUXQIJLXJXBUXJUWKUXNVUAVUCUXJUVLUXIUVCUXMUVLUXIUXMX
        NWCZVMUXJUWJUXOUYTUXJUVLUXIUVDVUDVMVSXDVTWAWDXGWFWFWBWCWFXHXIWIXKUUCDTO
        ZETOZHTOZUIZUULTOZUUNUWFUKVUHGXLRZTOZGXORZTOZGXMRZTOZVUEVUKVUFVUMVUGVUO
        DVUJTJXPEVULTIXPHVUNTKXPXQGXLXRGXOXRZGXMXRXSUUCUULUUFUXIUXIXTZOZUUJQZAB
        CUFZYAVUTTOVUIUUCUUKVUSABCUUCUUHVURUUJUUCUUGVUQUUFUUCFYFZUUGVUQYAZFYBUX
        IYAZUUCUWGVVAVVBVVCUIUWHEFGUXIUXQILYCVAYDYEYGYHVUTUWPVUQOZUUJQZABCUFZTV
        USVVEABCVURVVDUUJVURUWPVUQVBZOVVDUUEUUDVUQUXAUXBUSVVGVUQUWPUXIUXIYIYJYP
        VCVDVUQTOVUQVLVVFTOUXIUXIEEVULTIVUPYKYLZVVHYMUXIUXIYNABCUUITVUQYOYQYKUU
        LVUTTYRYSTTEDUULNMTTHUVLUVAUAUVLUNUWIYTUUAUUB $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d C a f g h x y z $.  $d D a f g h x y z $.  $d J a f x y z $.
      $d R a f g h x y z $.  $d T a f g h x y z $.
      $( The dual of a category is a category.  Joy of cats 3.5 (Contributed by
         FL, 4-Apr-2010.) $)
      dualcat2 $p |- ( T e. Cat ->
      <. <. C , D >. , <. J , { <. <. x , y >. , z >. |
          ( <. y , x >. e. dom R /\ z = ( y R x ) ) } >. >. e. Cat ) $=
        ( wcel co wceq wa cfv wi adantr cvv vh vg vf va ccat cop cv copab2 cded
        cdm wral catded dualded syl w3a calg dcsda eleq2 3anbi123d biimprd eqid
        dedalg cmpasso ex syl9 pm2.43i imp com12 eqcoms 3exp2 com24 imp32 imp31
        3syl simp12 wb anbi12d biimpcd com3l adantl impcom 3adant2 eqcom biimpi
        3adant3 3ad2ant3 3jca dualcat2lem opreq1 simplll simpl2 biimpd 3ad2ant1
        3exp imp41 cdom_ dmeqi cmpmorp syl6 3imp jca id ad2antll biimpa adantld
        com13 a1d 3exp1 eqtr4d syl111anc sylan9eqr mpdan eqcomd eleq2d ad2antrr
        codcmpc opreq2 simpl sylc simpll domcmpc syl2an ad2antrl eqtrd 3eqtr4rd
        syl121anc exp31 r19.21aiv r19.21aivv cid_ adantrr pm5.74da mpbird ccod_
        jdmo eleq1i fvex wss visset bitri simplrl syl11anc simprd simprl cmpidb
        simpr idosc cmpida 3expib anbi2d imbi1d simpld eqtr3 sylan eqeq1d bitrd
        simplrr jca31 3anbi123i mpbir3an cxp wfun cmppfc simp2d sseld ssoprab2g
        anim1d dmex eqeltri oprabex2 ccnv eqcomi eleq2i opelcnv anbi1i oprabbii
        crn cnvxp twsvbdop mpbir ssexg sylancl iscat sylancr ) GUEMZDEUFHBUGZAU
        GZUFZFUJZMZCUGUWFUWGFNZOZPZABCUHZUFUFZUEMZUWOUIMZUAUGZDQZUBUGZEQZOZUWTD
        QZUCUGZEQZOZPZUWRUWTUXDUWNNZUWNNZUWRUWTUWNNZUXDUWNNZOZRZUADUJZUKZUBUXNU
        KUCUXNUKZPUXEUDUGZOZUXQHQZUXDUWNNZUXDOZRZUCUXNUKUDHUJZUKZUXDDQZUXQOZUXD
        UXSUWNNZUXDOZRZUCUXNUKUDUYCUKZPZPZUWEUWQUXPUYKUWEGUIMZUWQGULZABCDEFGHIJ
        KLUMUNUWEUXOUCUBUXNUXNUWEUXDUXNMZUWTUXNMZPZUXOUWEUYQPZUXMUAUXNUYRUWRUXN
        MZUXGUXLUYRUYSPZUXGPZUXDUWTUWRFNZFNZUXDUWTFNZUWRFNZUXKUXIUYRUYSUXGVUCVU
        EOZUWEUYOUYPUYSUXGVUFRZRUWEUYSUYPUYOVUGUWEUYSUYPUYOVUGUXGUWEUYSUYPUYOUO
        ZPZVUFUXBUXFVUIVUFRZUXFVUJRUXAUWSUXFUXAUWSOZVUJVUKVUJRUXEUXCUXEUXCOZVUK
        VUJVUIVULVUKPZVUFUWEVUHVUMVUFRZUWEVUHVUNRZUWEUYMGUPMZUWEVUORZUYNGVBZVUP
        EUJZUXNOZVUQDEGIJUQZVUTVUHUWRVUSMZUWTVUSMZUXDVUSMZUOZUWEVUNVUTVVEVUHVUT
        VVBUYSVVCUYPVVDUYOVUSUXNUWRURVUSUXNUWTURVUSUXNUXDURUSUTUWEVVEVUNDEFGUWR
        UWTUXDVUSVUSVAZIJLVCVDVEUNVNVFVGVHVDVIVHVIVGVHVJVKVLVMVUAUXJVUBOZUXKVUC
        OVUAUWEVVBVVCPZVUKUOZVVGUWEUYQUYSUXGVVIUWEUYQUYSUXGVVIRRZRZUWEUYMVUPUWE
        VVKRZUYNVURVUPVUTVVLVVAVUTUWEUYQVVJVUTUWEUYQUOZUYSUXGVVIVVMUYSUXGUOZUWE
        VVHVUKVUTUWEUYQUYSUXGVOVVMUYSVVHUXGVVMUYSVVHVUTUYQUYSVVHRZUWEUYQVUTVVOU
        YPVUTVVORUYOUYSUYPVUTVVHUYSUYPVUTVVHRVUTUYSUYPPZVVHVVPVVHVPUXNVUSUXNVUS
        OZUYSVVBUYPVVCUXNVUSUWRURZUXNVUSUWTURZVQVIVRVDVSVTWAWBVGWEUXGVVMVUKUYSU
        XBVUKUXFUXBVUKUWSUXAWCWDZSWFWGWNWNUNVNVFWOABCDEFGUWRUWTIJLWHUNVVGVUAUXK
        VUBUXDUWNNZVUCUXJVUBUXDUWNWIVUAUWEVUBVUSMZVVDPZUXEVUBDQZOVWAVUCOUWEUYQU
        YSUXGWJZUWEUYQUYSUXGVWCUWEUYQUYSUXGVWCRRZRZUWEUYMVUPUWEVWGRZUYNVURVUPVU
        TVWHVVAVUTUWEUYQVWFVVMUYSUXGVWCVVNVWBVVDVVMUYSUXGVWBUXGUYSVVMVWBUXBUYSV
        VMVWBRRZUXFVWIUXAUWSVVMUYSVUKVWBVVMUYSUWEVVBVVCUOZVUKVWBRVVMUYSVWJVVMUY
        SPZUWEVVBVVCVUTUWEUYQUYSWKZVVMUYSVVBVUTUWEUYSVVBRZUYQVWMUXNVUSVVQUYSVVB
        VVRWLVIWMVGVVMVVCUYSVUTUYQVVCUWEUYQVUTVVCUYPVUTVVCRUYOVUTUYPVVCUYPVVCRU
        XNVUSVVQUYPVVCVVSWLVIZVHVTWAWBSWGVDDEFGUWRUWTVUSEGWPQZIWQZIJLWRWSXFVISX
        FWTVVMUYSVVDUXGVUTUYQVVDUWEUYQVUTVVDUYOVUTVVDRUYPVUTUYOVVDUYOVVDRUXNVUS
        VVQUYOVVDUXNVUSUXDURWLVIVHSWAWBWMXAWNWNUNVNVFWOVUAUXEUXCVWDUXFVULUYTUXB
        VULUXEUXCVULXBVIXCUXGUYTVWDUXCOZUXBUYTVWQRZUXFVWRUXAUWSUYTVUKVWQUYTVWJV
        UKVWQRUWEUYQUYSVWJUWEUYQUYSVWJRRZUWEUYMVUPUWEVWSRZUYNVURVUPVUTVWTVVAVUT
        UWEUYQUYSVWJVWKUWEVVBVVCVWLVVMUYSVVBVUTUWEUYSVVBVPZUYQVXAUXNVUSVVRVIWMX
        DVVMVVCUYSVUTUWEUYQVVCVUTUYQVVCRUWEVUTUYPVVCUYOVWNXEXGWTSWGXHUNVNVFVMDE
        FGUWRUWTVUSVVFIJLXPUNVHVISWAXIABCDEFGVUBUXDIJLWHXJXKXLVUAUXHVUDOZUXIVUE
        OVUAUWEVVCVVDVULVXBVWEUYRVVCUYSUXGUYQUWEVVCUYPUWEVVCRUYOUWEUYPVVCUWEUYM
        VUPUYPVVCVPUYNVURVUPUXNVUSUWTVUPVUSUXNVVAXMZXNVNVRVTWAZXOUYRVVDUYSUXGUY
        QUWEVVDUYOUWEVVDRZUYPUWEUYOVVDUWEUXNVUSUXDUWEUYMVUPVVQUYNVURVXCVNZXNZVR
        ZSWAZXOUXFVULUYTUXBUXFVULUXCUXEWCWDZXCZABCDEFGUWTUXDIJLWHYFVXBVUAUXIUWR
        VUDUWNNZVUEUXHVUDUWRUWNXQVUAUWEVVBVUDVUSMZVUDEQZUWSOVXLVUEOVWEUYTVVBUXG
        UYRUYSVVBUWEVWMUYQUWEUYSVVBUWEUXNVUSUWRVXFXNWLSVGSVUAUWEVVCVVDUOZVULVXM
        UYRVXOUYSUXGUYRUWEVVCVVDUWEUYQXRVXDVXIWGXOVXKDEFGUWTUXDVUSVWPIJLWRXSVUA
        VXNUXAUWSUYTVXOVULVXNUXAOZUXGUYTUWEVVCVVDUWEUYQUYSXTUYRVVCUYSVXDSUYRVVD
        UYSVXISWGUXFVULUXBVXJVTVXOVULVXPDEFGUWTUXDVUSVVFIJLYAVGYBUXBVUKUYTUXFVV
        TYCYDABCDEFGUWRVUDIJLWHYFXKXLYEYGYHVDYIUWEUYDUYJUWEUYBUDUCUYCUXNUWEUXQU
        YCMZUYOPZUXRUYAUWEVXRPZUXRPZUXTUXDUXSFNZUXDVXTUWEUXSVUSMZVVDUXEUXSDQZOU
        XTVYAOUWEVXRUXRXTZVXSVYBUXRUWEVXQVYBUYOUXQGHVUSUYCVWPHGYJQZKWQKYOZYKSVX
        SVVDUXRVXRUWEVVDUYOVXEVXQVXHVTWASVXTUXEUXQVYCVXSUXRUUFVXTUXSEQZUXQOZVYC
        UXQOZVXTUWEVXQVYHVYIPVYDUWEVXQUYOUXRUUAUXQDEGHUYCUYCVAZIKJUUGZUUBUUCXIA
        BCDEFGUXSUXDIJLWHYFVXSUXRVYAUXDOZVXSUWEVXQVVDUXRVYLRUWEVXRXRUWEVXQUYOUU
        DVXRUWEVVDUYOVXEVXQUWEUYOVVDUWEUXNVUSUXDUWEVUSUXNUWEVUPVUTUWEUYMVUPUYNV
        URUNVVAUNXMXNVRVTWAUXQEFGUXDHVUSUYCVVFIVYJKLUUEXJVGYDYGYIUWEUYIUDUCUYCU
        XNUWEVXRUYIRZVXQVVDPZUYFUXSUXDFNZUXDOZRZRZUWEVXQVVDVYQUXQDEFGUXDHVUSUYC
        VVFIVYJKLJUUHUUIUWEVYMVYNUYIRVYRUWEVXRVYNUYIUWEUYOVVDVXQVXGUUJUUKUWEVYN
        UYIVYQUWEVYNPZUYFUYHVYPVYSUYFPZUYGVYOUXDVYTUWEVVDVYBVYGUYEOZUYGVYOOUWEV
        YNUYFXTUWEVXQVVDUYFUUQVYSVYBUYFUWEVXQVYBVVDVYFYKSVYSVYHUYFWUAUWEVXQVYHV
        VDUWEVXQPVYHVYIVYKUULYKVYGUYEUXQUUMUUNABCDEFGUXDUXSIJLWHYFUUOYLYLUUPYMY
        IXAUURUWEDTMZETMZHTMZUOZUWNTMZUWPUYLVPWUEGYNQZTMZVWOTMZVYETMZWUBWUHWUCW
        UIWUDWUJDWUGTJYPEVWOTIYPHVYETKYPUUSGYNYQGWPYQZGYJYQUUTUWEUWNUWHVUSVUSUV
        AZMZUWLPZABCUHZYRWUOTMZWUFUWEUWMWUNABCUWEUWJWUMUWLUWEUWIWULUWHUWEFUVBUW
        IWULYRFUVQVUSYREFGVUSVVFILUVCUVDUVEUVGUVFWUPUWGVUSMUWFVUSMPUWLPABCUHZTM
        ZABCVUSVUSUWKWUQVUSVWOUJTVWPVWOWUKUVHUVIZWUSWUQVAUVJWUPUWGUWFUFWULMZUWL
        PZABCUHZTMWURWUOWVBTWUNWVAABCWUMWUTUWLWUMUWHWULUVKZMWUTWULWVCUWHWVCWULV
        USVUSUVRUVLUVMUWFUWGWULBYSAYSUVNYTUVOUVPYPWVBWUQTUWLABCVUSVUSUVSYPYTUVT
        UWNWUOTUWAUWBTTEDUWNUCUBUATTHUXNUYCUDUXNVAVYJUWCUWDYM $.
        $( [18-Apr-2010] $) $( [4-Apr-2010] $)
    $}
  $}


