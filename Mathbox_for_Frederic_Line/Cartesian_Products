$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Cartesian Products
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the projection symbol. $)
  $c pr $.

  $( Extend class notation to include the projection mapping. $)
  cpro $a class pr $.

  $( Declare the projection symbol. $)
  $c prj $.

  $( Extend class notation to include the projection mapping. $)
  cproj $a class prj $.

  ${
    $d x y z f g $.
    $( Definition of a projection (also called a coordinate mapping).
       Meaninful if ` x ` is a cartesian product and ` y ` is an index.

       In what follows I will call nuple an element of a cartesian product.

       If ` X ` is a cartesian product, ` N ` a nuple of ` X ` , ` I ` an
       indice, ` ( ( X pr I ) `` N ) ` is the ` I ` th coordinate of the nuple
       ` N ` .

       Suppose the set of indices is ` { 1 , 2 } ` and ` X ` is the cartesian
       product
       ` { { <. 1 , a >. , <. 2 , u >. } , { <. 1 , a >. , <. 2 , v >. } } `
       then ` ( ( X pr 1 ) `` { <. 1 , a >. , <. 2 , u >. } ) = a ` and
       ` ( ( X pr 2 ) `` { <. 1 , a >. , <. 2 , u >. } ) = u ` . $)
    df-pro $a |-
      pr = { <. <. x , y >. , z >. |
         z = { <. f , g >. | ( f e. x /\ g = ( f ` y ) ) } } $.
  $}

  ${
    $d x y z f g $.
    $( Definition of a projection.  Meaninful if ` x ` is a cartesian product
       and ` y ` is a set of indices.  Suppose
       ` X = { { <. 1 , a >. , <. 2 , u >. } , { <. 1 , a >. , <. 2 , v >. } }
       ` then ` ( X prj 1 ) = { a } ` and ` ( X prj 2 ) = { u , v } ` . $)
    df-prj $a |-
      prj = { <. <. x , y >. , z >. |
         z = { <. f , g >. | ( f e. x /\ g = ( f |` y ) ) } } $.
  $}

  ${
    $d A f $.  $d B f $.  $d F f $.  $d f x $.
    $( A nuple is a function.  (Contributed by FL, 6-Jun-2011.) $)
    elixp2a $p |- ( F e. X_ x e. A B -> F Fn A ) $=
      ( vf cixp wcel wfn cv wi wceq eleq1 fneq1 imbi12d cfv wral visset simplbi
      elixp vtoclg pm2.43i ) DABCFZGZDBHZEIZUBGZUEBHZJUCUDJEDUBUEDKUFUCUGUDUEDU
      BLBUEDMNUFUGAIUEOCGABPABCUEEQSRTUA $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A f $.  $d B f $.  $d F f x $.
    $( The base class of the elements of a nuple.  (Contributed by FL,
       6-Jun-2011.) $)
    elixp2b $p |- ( F e. X_ x e. A B -> A. x e. A ( F ` x ) e. B ) $=
      ( vf cixp wcel cv cfv wral wi wceq eleq1 fveq1 eleq1d ralbidv imbi12d wfn
      visset elixp simprbi vtoclg pm2.43i ) DABCFZGZAHZDIZCGZABJZEHZUDGZUFUJIZC
      GZABJZKUEUIKEDUDUJDLZUKUEUNUIUJDUDMUOUMUHABUOULUGCUFUJDNOPQUKUJBRUNABCUJE
      STUAUBUC $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A x $.  $d C x $.  $d F x $.  $d I x $.
    bcelnu.1 $e |- ( x = I -> B = C ) $.
    $( The base class of an element of a nuple.  (Contributed by FL,
       19-Jun-2011.) $)
    bclelnu $p |- ( ( F e. X_ x e. A B /\ I e. A ) -> ( F ` I ) e. C ) $=
      ( cixp wcel cv cfv wral elixp2b wceq fveq2 eleq12d rcla4va ex mpan9 ) EAB
      CHIAJZEKZCIZABLZFBIZFEKZDIZABCEMUDUCUFUBUFAFBTFNUAUECDTFEOGPQRS $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d I f g x y z $.  $d P f g x y z $.
    $( Definition of the coordinate mapping (or projection ) of index ` I ` .
       ` P ` is a cartesian product.  (Contributed by FL, 19-Jun-2011.) $)
    ispr1 $p |- ( ( P e. Q /\ I e. J ) -> ( P pr I ) =
      { <. f , g >. | ( f e. P /\ g = ( f ` I ) ) } ) $=
      ( vx vy vz wcel wa cvv cv cfv wceq copab cpro elisset opabbidv copab2 cxp
      anim12i opabex2g adantr eleq2 anbi1d fveq2 eqeq2d anbi2d cres df-pro wrel
      co cdm reloprab reldmoprab resid2 resoprab 3eqtr2i oprabval2g 3expia sylc
      mp2an ) ABJZEFJZKALJZELJZKCMZAJZDMZEVHNZOZKZCDPZLJZAEQUMVNOZVDVFVEVGABREF
      RUBVDVOVECDAVKBUCUDVFVGVOVPGHIAELLVHGMZJZVJHMZVHNZOZKZCDPZVNQVIWAKZCDPLVQ
      AOZWBWDCDWEVRVIWAVQAVHUEUFSVSEOZWDVMCDWFWAVLVIWFVTVKVJVSEVHUGUHUISQIMWCOZ
      GHITZWHLLUAUJZVQLJVSLJKWGKGHITGHICDUKWHULWHUNULWIWHOWGGHIUOWGGHIUPWHUQVCW
      GGHILLURUSUTVAVB $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f x $.  $d C f x y $.  $d I f x y $.  $d P f y $.
    prmapcp2.1 $e |- P = X_ x e. A B $.
    prmapcp2.2 $e |- ( x = I -> B = C ) $.
    $( A projection is a mapping from a cartesian product to an element of the
       family implied in the product.  Bourbaki E.II.34 cor. 1.  (Contributed
       by FL, 19-Jun-2011.) $)
    prmapcp2 $p |- ( ( P e. Q /\ I e. A )
      -> ( P pr I ) : P --> C ) $=
      ( vf vy wcel wa cv cfv wral cpro co wf wceq wi eleq2i bclelnu sylbi com12
      cixp ex r19.21aiv adantl copab wb ispr1 fopab2g syl mpbid ) EFLZGBLZMZGJN
      ZOZDLZJEPZEDEGQRZSZUQVBUPUQVAJEUSELZUQVAVEUSABCUFZLZUQVAUAEVFUSHUBVGUQVAA
      BCDUSGIUCUGUDUEUHUIURVCVEKNUTTMJKUJTVBVDUKEFJKGBULJKEDUTVCUMUNUO $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d I f g $.  $d P f g $.
    $( A projection is a mapping whose domain is a cartesian product.
       (Contributed by FL, 20-Jun-2011.) $)
    prmapcp3 $p |- ( ( P e. Q /\ I e. A )
      -> ( P pr I ) Fn P ) $=
      ( vf vg wcel wa cpro co cfv wceq copab wfn ispr1 fvex eqid fnopab2 fneq1
      cv mpbiri syl ) BCGDAGHBDIJZETZBGFTDUDKZLHEFMZLZUCBNZBCEFDAOUGUHUFBNEFBUE
      UFDUDPUFQRBUCUFSUAUB $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d F f g $.  $d I f g $.  $d P f g $.
    $( The ` I ` th coordinate of the nuple ` F ` .  (Contributed by FL,
       19-Jun-2011.) $)
    valpr $p |- ( ( P e. Q /\ I e. J /\ F e. P )
      -> ( ( P pr I ) ` F ) = ( F ` I ) ) $=
      ( vf vg wcel w3a cpro co cfv cv wceq wa copab ispr1 3adant3 fveq1d cvv
      fvex fveq1 eqid fvopab4g mpan2 3ad2ant3 eqtrd ) ABHZDEHZCAHZIZCADJKZLCFMZ
      AHGMDUMLZNOFGPZLZDCLZUKCULUOUHUIULUONUJABFGDEQRSUJUHUPUQNZUIUJUQTHURDCUAF
      GCUNUQATUODUMCUBUOUCUDUEUFUG $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A x $.  $d x y $.
    $( The variable x is bound in ` X_ x e. A B ` .  (Contributed by FL,
       20-Jun-2011.) $)
    hbcp $p |- ( y e. X_ x e. A B -> A. x y e. X_ x e. A B ) $=
      ( cv cixp wcel wfn cfv wral wa visset elixp ax-17 hbra1 hban hbxfrbi ) BE
      ZACDFGRCHZAERIDGZACJZKAACDRBLMSUAASANTACOPQ $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d A f x y $.  $d B y $.  $d F f g x y $.  $d P f g $.  $d Q f x y $.
    npincppr.1 $e |- P = X_ x e. A B $.
    $( A set of nuples is included in the cartesian product of the projections
       of the nuples.  Bourbaki E.II.32.  (Contributed by FL, 20-Jun-2011.) $)
    npincppr $p |- ( ( F C_ P /\ P e. Q )
      -> F C_ X_ x e. A ( ( P pr x ) " F ) ) $=
      ( vf vy vg wss wcel wa cv wral wi cfv sylbi imp ex cpro cima dfss3 eleq2i
      co cixp wfn visset elixp simpll eleq1i sseq2i w3a ax-17 hbcp hbss hbxfrbi
      hbel hb3an hban wceq wrex ssel valpr 3exp com3r syl6com com4l 3adant3 a1d
      eqcomi 3imp fveq2 eqeq1d rcla4ev sylcom com24 wb prmapcp3 3ad2ant2 adantr
      imp41 biimpi 3ad2ant1 ad2antrr fvelimab syl11anc mpbird 3exp1 com13 imp31
      ralimdaa pm2.43i com12 adantl sylanbrc ralimdva sylibr ) FDKZDELZMHNZABDA
      NZUAUEZFUBZUFZLZHFOZFXEKWSWTXGWSWTXGPZWSXADLZHFOZWSXHPHFDUCWTWSXJXGWTWSXJ
      XGPWTWSMZXIXFHFXIXKXAFLZMZXFXIXAABCUFZLZXMXFPZDXNXAGUDXOXABUGZXBXAQZCLZAB
      OZMZXPABCXAHUHZUIYAXMXFYAXMMXQXRXDLZABOZXFXQXTXMUJYAXMYDXTXMYDPXQXMXTYDWT
      WSXLXTYDPZWTWSXLYEPZPZWTXNELZWTYGPDXNEGUKZWSWTYHYFWSFXNKZWTYHYFPPDXNFGULY
      JWTYHXLYEYJWTYHUMZXLMZXSYCABYKXLAYJWTYHAAIFXNINZFLAUNAIBCUOZUPWTYHAYIAIIX
      NEYNYMELAUNURZUQYOUSXLAUNUTYLXBBLZMZXSYCYQXSMZYCJNZXCQZXRVAZJFVBZYKXLYPXS
      UUBYKXSYPXLUUBYKXSYPXLUUBPYKXSYPUMXLXAXCQZXRVAZUUBYKXSYPXLUUDPZYKYPUUEPZX
      SYJWTUUFYHYJWTUUFXLYJWTYPUUDYJXLXOWTYPUUDPPZFXNXAVCXOXIUUGXNDXADXNGVKZUDW
      TYPXIUUDWTYPXIUUDDEXAXBBVDVEVFRVGVHSVIVJVLXLUUDUUBUUAUUDJXAFYSXAVAYTUUCXR
      YSXAXCVMVNVOTVPVEVQWBYRXCDUGZWSYCUUBVRYQUUIXSYLYPUUIYKYPUUIPZXLWTYJUUJYHW
      TYPUUIBDEXBVSTVTWASWAYLWSYPXSYKWSXLYJWTWSYHYJWSXNDFUUHULWCWDWAWEJDFXRXCWF
      WGWHTWLWIRWJRWMWKWNWOSABXDXAYBUIWPTRRWNWQTWJRWMSHFXEUCWR $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d F x $.  $d I x $.  $d J x $.
    repfuntw.1 $e |- I e. A $.
    repfuntw.2 $e |- J e. B $.
    $( Representation as a set of pairs of a function whose domain has two
       distinct elements.  (Contributed by FL, 26-Jun-2011.) $)
    repfuntw $p |- ( I =/= J -> ( F Fn { I , J } <->
       F = { <. I , ( F ` I ) >. , <. J , ( F ` J ) >. } ) ) $=
      ( vx cfv wceq wa wi csn cun cima cab cuni c0 cdm a1i wne cpr wfn cop wral
      cv wcel wo elprg cin dmsnop ineq1 necom disjsn2 sylbi eqeq1 syl5ibr ax-mp
      syl adantr imadisj sylibr uneq2 syl6eq eqeq1d abbidv unieqd elisseti fvex
      un0 fvsn df-fv eqtr3i syl6reqr imaundir eqcomi eqeq1i abbii unieqi fveq1d
      df-pr 3eqtrd fveq2 eqeq12d incom imaeq1i jaoi syl6bi pm2.43i r19.21aiv wb
      uncom com12 simpr wfun funpr dmeqi dmun uneq12i eqtr4i 3eqtri jctir df-fn
      eqfnfv2 syl11anc mpbird eqtr ineq12d eqtrd funsn pm3.2i jctil funun funeq
      biimpd eqcoms 3syl syl5com mpan2 impcom dmeq adantl sylanbrc impbida ) DE
      UAZCDEUBZUCZCDDCIZUDZEECIZUDZUBZJZYEYGKZYMHUFZCIZYOYLIZJZHYFUEZYNYRHYFYOY
      FUGZYNYRYTYNYRLZYTYTYODJZYOEJZUHUUAYODEYFUIUUBUUAUUCYNYRUUBYHDYLIZJYNYHYI
      MZYKMZNZDMZOZYOMZJZHPZQZDUUGIZUUDYNYHUUEUUHOZUUFUUHOZNZUUJJZHPZQZUUMYNUUP
      RJZYHUUTJYNUUFSZUUHUJZRJZUVAYEUVDYGUVBEMZJZYEUVDLZEYJUKZUVFUVCUVEUUHUJZJZ
      UVGUVBUVEUUHULYEUVDUVJUVIRJZYEEDUAUVKDEUMEDUNUOZUVCUVIRUPUQUSURUTUUFUUHVA
      VBUVAUUTUUOUUJJZHPZQZYHUVAUUSUVNUVAUURUVMHUVAUUQUUOUUJUVAUUQUUORNUUOUUPRU
      UOVCUUOVJVDVEVFVGDUUEIYHUVODYHDAFVHZDCVIVKHDUUEVLVMVNUSUUSUULUURUUKHUUQUU
      IUUJUUIUUQUUEUUFUUHVOVPVQVRVSVDUUMUUNJYNUUNUUMHDUUGVLVPTYNDUUGYLUUGYLJYNY
      LUUGYIYKWAZVPTZVTWBUUBYPYHYQUUDYODCWCYODYLWCWDUQYNYRUUCYJEYLIZJYNYJUUGUVE
      OZUUJJZHPZQZEUUGIZUVSYNYJUUFUVEOZUUEUVEOZNZUUJJZHPZQZUUFUUENZUVEOZUUJJZHP
      ZQZUWCYNUWFRJZYJUWJJYNUUESZUVEUJZRJZUWPYEUWSYGUWQUUHJZYEUWSLZDYHUKZUWTUWR
      UVIJZUXAUWTUWRUUHUVEUJZUVIUWQUUHUVEULUUHUVEWEVDYEUWSUXCUVKUVLUWRUVIRUPUQU
      SURUTUUEUVEVAVBUWPUWJUWEUUJJZHPZQZYJUWPUWIUXFUWPUWHUXEHUWPUWGUWEUUJUWPUWG
      UWERNUWEUWFRUWEVCUWEVJVDVEVFVGEUUFIYJUXGEYJEBGVHZECVIVKHEUUFVLVMVNUSUWJUW
      OJYNUWIUWNUWHUWMHUWGUWLUUJUWLUWGUUFUUEUVEVOVPVQVRVSTUWOUWCJYNUWNUWBUWMUWA
      HUWLUVTUUJUWKUUGUVEUUFUUEWLWFVQVRVSTWBUWCUWDJYNUWDUWCHEUUGVLVPTYNEUUGYLUV
      RVTWBUUCYPYJYQUVSYOECWCYOEYLWCWDUQWGWHWIWMWJYNYGYLYFUCZYMYSWKYEYGWNYEUXIY
      GYEYLWOZYLSZYFJZKUXIYEUXJUXLDEYHYJUVPUXHWPUXKUUGSUWQUVBNZYFYLUUGUVQWQUUEU
      UFWRUXMUUHUVENYFUWQUUHUVBUVEUXBUVHWSDEWAWTXAZXBYLYFXCVBUTHYFCYLXDXEXFYEYM
      KCWOZCSZYFJZYGYMYEUXOYMYLUUGJZYEUXOLUVQYMUXRKCUUGJZYEUXOCYLUUGXGYEUUEWOZU
      UFWOZKZUWQUVBUJZRJZKUUGWOZUXSUXOLYEUYDUYBYEUYCUXDRYEUWQUUHUVBUVEUWTYEUXBT
      UVFYEUVHTXHDEUNXIUXTUYADYHUVPXJEYJUXHXJXKXLUUEUUFXMUXSUYEUXOUYEUXOLUUGCUU
      GCJUYEUXOUUGCXNXOXPWMXQXRXSXTYMUXQYEYMUXPUXKYFCYLYAUXNVDYBCYFXCYCYD $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d x y M $.  $d x A f a b $.  $d x f I a b $.  $d x f J a b $.  $d x y N $.
    $d B f a b $.  $d M a b $.  $d N a b $.
    repcpwti.1 $e |- A = { I , J } $.
    repcpwti.2 $e |- B = if ( x = I , M , N ) $.
    repcpwti.3 $e |- I e. C $.
    repcpwti.4 $e |- J e. D $.
    $( A representation of a cartesian product with two indices.  (Contributed
       by FL, 26-Jun-2011.) $)
    repcpwti $p |- ( I =/= J
                -> X_ x e. A B = { f
                    | E. a
                       e. M
                      E. b
                       e. N
                          f = { <. I , a >. , <. J , b >. } } ) $=
      ( wcel wceq wi cvv vy wne cixp cv wfn cfv wral wa cab cop cpr wrex df-ixp
      raleqi elisseti ralpr bitri csb sbcel12g biimpd ax-mp csbfvg ax-17 iftrue
      wsbc cif syl5eq csbief eleq12i wb a1i wal neeq2 eqcoms df-ne eqcom notbii
      iffalse sylbi syl6com 19.21aiv csbieb sylib eleq12d fneq2i repfuntw opeq2
      wn biimpcd preq2d eqeq2d rcla4ev preq1d adantr rexbidva ex syl5com expcom
      com3r com34 syl6bi pm2.43a com4l com12 syl imp impcom w3a fnprg 3exp 3imp
      com3l sylibr com13 mpan imp31 visset elpr fvsn2a eleq1a syl5 eleq2 eleq2i
      wo syl5ibr syl6ibr fveq2 eleq1d sylibrd fvsn2b necom simpr syl6bir imbi2d
      eleq1 mpcom jaoi eleq2s r19.21aiv jca fneq1 anbi12d syl5ibrcom r19.23aivv
      fveq1 ralbidva impbid abbidv ) GHUBZABCUCFUDZBUEZAUDZUUJUFZCQZABUGZUHZFUI
      UUJGKUDZUJZHLUDZUJZUKZRZLJULZKIULZFUIABCFUMUUIUUPUVDFUUIUUPUVDUUPUUIUVDUU
      OUUKUUIUVDSZUUOUUNAGVEZUUNAHVEZUHZUUKUVESZUUOUUNAGHUKZUGUVHUUNABUVJMUNUUN
      AGHGDOUOZHEPUOZUPUQUVFUVGUVIUVFAGUUMURZAGCURZQZUVGUVISZGTQZUVFUVOSUVKUVQU
      VFUVOAGUUMCTUSUTVAUVOGUUJUFZIQZUVPUVMUVRUVNIUVQUVMUVRRUVKAGTUUJVBVAAUAGCI
      UVKUAUDZIQAVCUULGRZCUWAIJVFZINUWAIJVDZVGVHVIUVGUVSUVIUVGAHUUMURZAHCURZQZU
      VSUVISHTQZUVGUWFVJUVLAHUUMCTUSVAUUIUWFUVSUUKUVDUWFUUIUVSUUKUVDSSZUUIUWFHU
      UJUFZJQZUUIUWHSUUIUWDUWIUWEJUWDUWIRZUUIUWGUWKUVLAHTUUJVBVAVKUUIUULHRZCJRZ
      SZAVLUWEJRUUIUWNAUWLUUIGUULUBZUWMUWLUUIUWOUUIUWOVJHUULHUULGVMVNUTUWOGUULR
      ZWHZUWMGUULVOUWQCUWBJNUWQUWAWHZUWBJRZUWPUWAGUULVPVQUWAIJVRZVSVGVSVTWAAUAH
      CJUVLUVTJQAVCWBWCWDUWJUUIUUKUVSUVDUUIUUKUWJUVSUVDSZUUKUUIUUJGUVRUJZHUWIUJ
      ZUKZRZUWJUXASUUKUUJUVJUEZUUIUXESBUVJUUJMWEUUIUXFUXEDEUUJGHOPWFWIVSUWJUXEU
      XAUWJUXEUHUUJUXBUUTUKZRZLJULZUVSUVDUXHUXELUWIJUUSUWIRZUXGUXDUUJUXJUUTUXCU
      XBUUSUWIHWGWJWKWLUVSUXIUVDUVCUXIKUVRIUUQUVRRZUVBUXHLJUXKUUSJQZUHUVAUXGUUJ
      UXKUVAUXGRUXLUXKUURUXBUUTUUQUVRGWGWMWNWKWOWLWPWQWRVTWSWTXAXBXCVSXDVSXEXFV
      SXGXDUVDUUIUUPUVBUUIUUPSZKLIJUUQIQZUXLUHZUXMUVBUUIUVABUEZUULUVAUFZCQZABUG
      ZUHZSUXOUUIUXTUXOUUIUHZUXPUXSUXNUXLUUIUXPUVQUXNUXLUUIUXPSZSUVKUXLUVQUXNUH
      ZUYBUWGUXLUYCUYBSUVLUUIUYCUWGUXLUHZUXPUUIUYCUYDUXPUUIUYCUYDXHUVAUVJUEZUXP
      UUIUYCUYDUYEUYCUUIUYDUYESZUVQUUIUYFSUXNUYDUVQUUIUYEUWGUVQUUIUYESZSUXLUVQU
      WGUYGUUIUVQUWGUYEUUIUVQUWGUYEGHUUQUUSTTXIXJXLXDWNXLWNXDXKBUVJUVAMWEXMXJXN
      XOXDXOXPUYAUXRABUULBQZUYAUXRUYAUXRSZUULUVJBUULUVJQUWAUWLYDUYIUULGHAXQXRUW
      AUYIUWLUWAUYAGUVAUFZCQZUXRUWAUYAUYJUWBQZUYKUWAUWBIRZUYAUYLSUWCUYAUYLUYMUY
      JIQZUXOUUIUYNUUIUYJUUQRZUXOUYNGHUUQUUSDETTOPKXQZLXQZXSUXNUYOUYNSUXLUUQIUY
      JXTWNYAXFUWBIUYJYBYEXECUWBUYJNYCYFUWAUXQUYJCUULGUVAYGYHYIUWLUYAHUVAUFZCQZ
      UXRUWLUYAUYRUWBQZUYSUYAUWLUYTUUIUXOUWLUYTSZUYRUUSRZUUIUXOVUASZGHUUQUUSDET
      TOPUYPUYQYJUUIVUCVUBUXOUWLUUSUWBQZSZSUWLUUIUXOVUDUWLUUIUWOUXOVUDSZUULHGVM
      UWOUULGUBZVUFGUULYKVUGUWRVUFUULGVOUWRUWSVUFUWTUXOVUDUWSUXLUXNUXLYLUWBJUUS
      YBYEXEVSVSYMXLVUBVUAVUEUXOVUBUYTVUDUWLUYRUUSUWBYOYNYNYEYPXGXDCUWBUYRNYCYF
      UWLUXQUYRCUULHUVAYGYHYIYQVSMYRXDYSYTWPUVBUUPUXTUUIUVBUUKUXPUUOUXSBUUJUVAU
      UAUVBUUNUXRABUVBUUNUXRVJUYHUVBUUMUXQCUULUUJUVAUUEYHWNUUFUUBYNUUCUUDXDUUGU
      UHVG $.
      $( [26-Jun-2011] $)
  $}

  ${
    $( Warning: a b x distincts seem curious. $)
    $d A a b f x $.  $d B a b f $.  $d F c d e f y $.  $d F e f x y $.
    $d I a b c d e f $.  $d I a b e f x $.  $d I c d e f y $.
    $d J a b c d e f $.  $d J a b e f x $.  $d J c d e f y $.
    $d M a b c d e f $.  $d M a b e f x $.  $d M c d e f y $.
    $d N a b c d e f $.  $d N a b e f x $.  $d N c d e f y $.
    cbcpcp.1 $e |- A = { I , J } $.
    cbcpcp.2 $e |- B = if ( x = I , M , N ) $.
    cbcpcp.3 $e |- F = ( a e. M , b e. N |-> { <. I , a >. , <. J , b >. } ) $.
    cbcpcp.4 $e |- I e. C $.
    cbcpcp.5 $e |- J e. D $.
    $( The canonical bijection between a cross product and a cartesian product
       (whose set of indices is composed of two different elements).  Bourbaki
       E.II.33 .  (Contributed by FL, 26-Jun-2011.) $)
    cbcpcp $p |- ( I =/= J -> F : ( M X. N ) -1-1-onto-> X_ x e. A B ) $=
      ( wceq wi wcel vy vf ve vc wne cxp wfn crn cixp cfv wral w3a wf1o cop cpr
      vd cv cvv wa prex a1i rgen2 fnoprab2ga mpbid wrex cab rngop elisset ax-mp
      wb repcpwti eqcomd eqtrd wex elxp df-opr opeq2 preq1d preq2d ovmpt2g eqtr
      co wo opex preq12b visset opth2 opeq12 ex com12 com3l syl imp opth1 df-ne
      wn pm2.21 sylbi adantr jaoi eqeq2 imbi1d imbi2d mpbird eqcoms mpsyl eqeq1
      3exp fveq2 eqeq2d imbi12d 19.23aivv eqeq1d r19.21aivv 3jca dff1o6 sylibr
      ) GHUEZFIJUFZUGZFUHZABCUIZRZAUQZFUJZUAUQZFUJZRZYDYFRZSZUAXSUKAXSUKZULXSYB
      FUMXRXTYCYKXRGKUQZUNZHLUQZUNZUOZURTZLJUKKIUKZXTYRXRYQKLIJYQYLITYNJTUSYMYO
      UTVAVBVAYRXTVJXRKLIJYPFOVCVAVDXRYAUBUQZYPRLJVEKIVEUBVFZYBYAYTRXRKLUBIJYPF
      OVGVAXRYBYTABCURURUBGHIJKLMNGDTGURTPGDVHVIZHETHURTQHEVHVIVKVLVMXRYJAUAXSX
      SYDXSTZYFXSTZUSXRYJUUBUUCXRYJSZUUBYDUCUQZYSUNZRZUUEITZYSJTZUSZUSZUBVNUCVN
      UUCUUDSZUCUBYDIJVOUUKUULUCUBUUGUUJUULUUGUUJUULSUUJUUCXRUUFFUJZYGRZUUFYFRZ
      SZSZSZSZUUSUUGUUCUUJUUQUUCYFUDUQZUPUQZUNZRZUUTITZUVAJTZUSZUSZUPVNUDVNUUJU
      UQSZUDUPYFIJVOUVGUVHUDUPUVCUVFUVHUVCUVFUVHSUVFUUJXRUUMUVBFUJZRZUUFUVBRZSZ
      SZSZSZUVOUVCGUUEUNZHYSUNZUOZURTZUVOUVPUVQUTUUJUVSUVFUVMUUHUUIUVSUVFUVMSZS
      UUHUUIUVSUVTUUEYSFWBZUUMRUUHUUIUVSULUWAUVRRZUVTUUEYSFVPKLUUEYSIJYPUVRFUVP
      YOUOURYLUUERYMUVPYOYLUUEGVQVRYNYSRYOUVQUVPYNYSHVQVSOVTUWBUVTSUUMUWAUUMUWA
      RZUWBUVTUWCUWBUSUUMUVRRZUVTUUMUWAUVRWAUWDUVTUVFXRUVRUVIRZUVKSZSZSZUWHUWDG
      UUTUNZHUVAUNZUOZURTZUWHUWIUWJUTUVFUWLUWGUVDUVEUWLUWGSUVDUVEUWLUWGUUTUVAFW
      BZUVIRUVDUVEUWLULUWMUWKRZUWGUUTUVAFVPKLUUTUVAIJYPUWKFUWIYOUOURYLUUTRYMUWI
      YOYLUUTGVQVRYNUVARYOUWJUWIYNUVAHVQVSOVTUWNUWGSUVIUWMUVIUWMRZUWNUWGUWOUWNU
      SUVIUWKRZUWGUVIUWMUWKWAUWPUWGXRUVRUWKRZUVKSZSZUWSUWPUWQXRUVKUWQUVPUWIRZUV
      QUWJRZUSZUVPUWJRZUVQUWIRZUSZWCXRUVKSZUVPUVQUWIUWJGUUEWDHYSWDGUUTWDHUVAWDW
      EUXBUXFUXEUWTUXAUXFUWTUUEUUTRZUXAUXFSGUUEGUUTUCWFUDWFWGUXAUXGUXFUXAYSUVAR
      ZUXGUXFSHYSHUVAUBWFUPWFWGXRUXHUXGUVKUXHUXGUVKSSXRUXGUXHUVKUXGUXHUVKUUEYSU
      UTUVAWHWIWJVAWKWLWJWLWMUXCUXFUXDUXCGHRZUXFGUUEHUVAUUAWNXRUXIUVKXRUXIWPUXI
      UVKSGHWOUXIUVKWQWRWJWLWSWTWRWJVAUWPUWFUWRXRUWPUWEUWQUVKUVIUWKUVRXAXBXCXDW
      LWIXEXFXHWMWJVIVAUWDUVMUWGUVFUWDUVLUWFXRUWDUVJUWEUVKUUMUVRUVIXGXBXCXCXDWL
      WIXEXFXHWMWKVIVAUVCUVHUVNUVFUVCUUQUVMUUJUVCUUPUVLXRUVCUUNUVJUUOUVKUVCYGUV
      IUUMYFUVBFXIXJYFUVBUUFXAXKXCXCXCXDWMXLWRWJVAUUGUULUURUUJUUGUUDUUQUUCUUGYJ
      UUPXRUUGYHUUNYIUUOUUGYEUUMYGYDUUFFXIXMYDUUFYFXGXKXCXCXCXDWMXLWRWMWJXNXOAU
      AXSYBFXPXQ $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d I f g x y z $.  $d P f g x y z $.
    $( Definition of a projection. ` I ` is a set of indices. ` P ` is a
       cartesian product.  (Contributed by FL, 19-Jun-2011.) $)
    isprj1 $p |- ( ( P e. Q /\ I e. J ) -> ( P prj I ) =
      { <. f , g >. | ( f e. P /\ g = ( f |` I ) ) } ) $=
      ( vx vy vz wcel wa cvv cv cres wceq copab cproj elisset opabbidv copab2
      anim12i opabex2g adantr eleq2 anbi1d reseq2 eqeq2d anbi2d cxp df-prj wrel
      co cdm reloprab reldmoprab resid2 resoprab 3eqtr2i oprabval2g 3expia sylc
      mp2an ) ABJZEFJZKALJZELJZKCMZAJZDMZVGENZOZKZCDPZLJZAEQULVMOZVCVEVDVFABREF
      RUAVCVNVDCDAVJBUBUCVEVFVNVOGHIAELLVGGMZJZVIVGHMZNZOZKZCDPZVMQVHVTKZCDPLVP
      AOZWAWCCDWDVQVHVTVPAVGUDUESVREOZWCVLCDWEVTVKVHWEVSVJVIVREVGUFUGUHSQIMWBOZ
      GHITZWGLLUINZVPLJVRLJKWFKGHITGHICDUJWGUKWGUMUKWHWGOWFGHIUNWFGHIUOWGUPVBWF
      GHILLUQURUSUTVA $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A x $.  $d I f g $.  $d P f g $.
    isprj2.1 $e |- P = X_ x e. A B $.
    $( Definition of a projection. ` I ` is a set of indices. ` P ` is a
       cartesian product.  (Contributed by FL, 19-Jun-2011.) $)
    isprj2 $p |- ( ( I e. J /\ A e. C /\ A. x e. A B e. D )
      -> ( P prj I ) = { <. f , g >. | ( f e. P /\ g = ( f |` I ) ) } ) $=
      ( wcel wral w3a cvv cproj co cv wceq wa cres cixp ixpexg syl5eqel 3adant1
      copab simp1 isprj1 syl11anc ) IJLZBDLZCELABMZNFOLZUJFIPQGRZFLHRUNIUASTGHU
      FSUKULUMUJUKULTFABCUBOKABCDEUCUDUEUJUKULUGFOGHIJUHUI $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f x y $.  $d B f y $.  $d C f $.  $d D f $.  $d I f x y $.
    $( A projection is a mapping from a cartesian product to one of its
       restriction.  (Contributed by FL, 19-Jun-2011.) $)
    prjmapcp $p |- ( ( I C_ A /\ A e. C /\ A. x e. A B e. D )
      -> ( X_ x e. A B prj I ) : X_ x e. A B --> X_ x e. I B ) $=
      ( vf vy wcel wral w3a cv cixp wa wfn cfv wi cvv 3ad2ant1 imp wss cproj co
      cres wf elixp2 fnssres ex 3ad2ant2 sylbi com12 visset elixp ssralv pm2.27
      eqcomd eleq1d biimpd ralimia syl6 adantl syl5com syl5bi resexg r19.21aiva
      fvres ax-mp sylanbrc wceq copab wb ssexg 3adant3 eqid isprj2 syld3an1 syl
      fopab2g mpbid ) FBUAZBDIZCEIABJZKZGLZFUDZAFCMZIZGABCMZJZWHWFWHFUBUCZUEZWC
      WGGWHWCWDWHIZNWEFOZALZWEPZCIZAFJZWGWCWLWMVTWAWLWMQWBWLVTWMWLWDRIZWDBOZWNW
      DPZCIZABJZKVTWMQZABCWDUFWSWRXCXBWSVTWMBFWDUGUHUIUJUKSTWCWLWQWLWSXBNZWCWQA
      BCWDGULZUMVTWAXDWQQWBVTXBXAAFJZQZXDWQXAAFBUNXBXGWQQWSXBXGXFWQXBXFUOXAWPAF
      WNFIZXAWPXHWTWOCXHWOWTWNFWDVFUPUQURUSUTVAVBSVCTAFCWEWRWERIXEWDFRVDVGUMVHV
      EWCWJWLHLWEVINGHVJVIZWIWKVKFRIZWAVTWBXIVTWAXJWBFBDVLVMABCDEWHGHFRWHVNVOVP
      GHWHWFWEWJVRVQVS $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f g x $.  $d A f x y $.  $d B f g $.  $d B f y $.  $d C f g x $.
    $d C f x y $.  $d D f g x $.  $d D f x y $.  $d E f g $.  $d E f y $.
    $d F f g $.
    cbicplem.1 $e |- F = ( f e. X_ x e. { A } B |-> ( f ` A ) ) $.
    cbicplem.2 $e |- ( x = A -> B = C ) $.
    $( Lemma for ~ cbicp . $)
    cbicplem $p |- ( ( A e. D /\ C e. E )
      -> F : X_ x e. { A } B -1-1-onto-> C ) $=
      ( vg vy wcel wa wceq cfv wi wral cvv a1i csn cixp wfn crn cv wf1o fvex wb
      w3a rgen fopab2ga mpbii wrex df-rex elixp2 3simpc visset simpl simpr 3jca
      cab wex impbii bitri anbi1i exbii syl5bb abbidv wal cmpt eqid mptfng mpbi
      rnmpt snidg adantr eqidd fvmptg eleq1a adantl mpd sylan eqcomd jca31 snex
      ex mptexg ax-mp fneq1 fveq1 eleq1d anbi12d eqeq2d cla4ev syl6 imp impbid1
      wsbc ralsng ax-17 fveq2 eleq12d sbciegf bitr2d anbi2d anbi1d exbidv bitrd
      19.23aiv 19.21aiv abeq2 sylibr 3eqtr4d fvmpt2 cmpbvb elsn eqeq12d biimprd
      im2anan9 sylbi r19.21aiv 3ad2ant3 jca elixp2a anim12i 3ad2ant1 eqfnfv syl
      com12 mpbird 3exp eqeq2 imbi1d imbi2d syl5ibr eqeq1 syl6com mp2an pm2.43i
      r19.21aivv dff1o6 ) BEMZDGMZNZHABUAZCUBZUCZHUDZDOZFUEZHPZKUEZHPZOZUUJUULO
      ZQZKUUFRFUUFRZUIUUFDHUFUUDUUGUUIUUQUUDBUUJPZSMZFUUFRZUUGUUSFUUFUUSUUJUUFM
      ZBUUJUGZTUJUUTUUGUHUUDFUUFUURHIUKTULUUDLUEZUUROZFUUFUMZLVAZUUJUUEUCZAUEZU
      UJPZCMZAUUERZNZUVDNZFVBZLVAZUUHDUUDUVEUVNLUVEUVAUVDNZFVBZUUDUVNUVDFUUFUNU
      VQUVNUHUUDUVPUVMFUVAUVLUVDUVAUUJSMZUVGUVKUIZUVLAUUECUUJUOUVSUVLUVRUVGUVKU
      PUVLUVRUVGUVKUVRUVLFUQTUVGUVKURUVGUVKUSUTVCVDVEVFTVGVHUUHUVFOUUDFLUUFUURH
      IVNTUUDUVCDMZUVNUHZLVIDUVOOUUDUWALUUDUVTUVGUURDMZNZUVDNZFVBZUVNUUDUVTUWEU
      UDUVTAUUEUVCVJZUUEUCZBUWFPZDMZNZUVCUWHOZNZUWEUUDUVTUWLUUDUVTNZUWGUWIUWKUW
      GUWMUVCSMZAUUERUWGUWNAUUEUWNUVHUUEMZLUQTUJAUUEUVCUWFUWFVKZVLVMTUUDBUUEMZU
      VTUWIUUBUWQUUCBEVOVPZUWQUVTNZUWHUVCOZUWIABUVCUVCUUEDUWFUVHBOZUVCVQUWPVRZU
      VTUWTUWIQUWQUVCDUWHVSVTWAWBUUDUWQUVTUWKUWRUWSUWHUVCUXBWCWBWDWFUWDUWLFUWFU
      UESMUWFSMBWEAUUEUVCSWGWHUUJUWFOZUWCUWJUVDUWKUXCUVGUWGUWBUWIUUEUUJUWFWIUXC
      UURUWHDBUUJUWFWJZWKWLUXCUURUWHUVCUXDWMWLWNWOUWDUVTFUWCUVDUVTUWBUVDUVTQUVG
      UURDUVCVSVTWPXIWQUUDUWDUVMFUUDUWCUVLUVDUUDUWBUVKUVGUUBUWBUVKUHUUCUUBUVKUV
      JABWRUWBUVJABEWSUVJUWBABEUWBUWBAVIQUUBUWBAWTTUXAUVIUURCDUVHBUUJXAZJXBXCXD
      VPXEXFXGXHXJUVNLDXKXLXMUUDUUPFKUUFUUFUVAUULUUFMZNZUUDUUPUXGUUDUUPQZUUSBUU
      LPZSMZUXGUXGUXHQZQUVBBUULUGUXGUUSUXJNUUKUUROZUUMUXIOZNUXKUVAUUSUXLUXFUXJU
      XMUVAUUSUXLFUUFUURSHIXNWFUXFUXJUXMKUUFUXISHFKUUFUURUXIHIBUUJUULWJXOXNWFXS
      UXLUXMUXKUXMUXKUXLUXGUUDUURUUMOZUUOQZQZQUXGUXPUXMUUDUURUXIOZUUOQZQUXGUUDU
      XQUUOUXGUUDUXQUIZUUOUUEUUEOZUVIUVHUULPZOZAUUERZNZUXSUXTUYCUXSUUEVQUXQUXGU
      YCUUDUXQUYBAUUEUWOUXQUYBUWOUXAUXQUYBQABXPUXAUYBUXQUXAUVIUURUYAUXIUXEUVHBU
      ULXAXQXRXTYIYAYBYCUXSUVGUULUUEUCZNZUUOUYDUHUXGUUDUYFUXQUVAUVGUXFUYEAUUECU
      UJYDAUUECUULYDYEYFAUUEUUEUUJUULYGYHYJYKUXMUXOUXRUUDUXMUXNUXQUUOUUMUXIUURY
      LYMYNYOUXLUXHUXPUXGUXLUUPUXOUUDUXLUUNUXNUUOUUKUURUUMYPYMYNYNYOWPYQYRYSYIY
      TUTFKUUFDHUUAXL $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A f g x $.  $d A f x y $.  $d B f g $.  $d B f y $.  $d C f x $.
    $d D f x $.  $d E f $.  $d F f y $.
    cbicp.1 $e |- F = ( g e. X_ x e. { A } B |-> ( g ` A ) ) $.
    cbicp.2 $e |- ( x = A -> B = C ) $.
    $( Canonical bijection between a cartesian product indexed by a singleton
       and the base class of the elements of the 1-uple.  Bourbaki E II.32
       (Contributed by FL, 6-Jun-2011.) $)
    cbicp $p |- ( ( A e. D /\ C e. E )
      -> F : X_ x e. { A } B -1-1-onto-> C ) $=
      ( vf csn cixp cv cfv cmpt eqid fveq1 cmpbvb eqtri cbicplem ) ABCDEKGHHFAB
      LCMZBFNZOZPZKUBBKNZOZPIFKUBUDUGUEUEQBUCUFRSTJUA $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.
    $( Projection of nuple.  (Contributed by FL, 7-Nov-2011.) $)
    prjnpl $p |- ( ( B C_ A /\ F e. X_ x e. A C )
       -> ( F |` B ) e. X_ x e. B C ) $=
      ( cixp wcel wss cres cvv wfn cv cfv wral w3a wi elixp2 wa com3r syl fvres
      fnssres ssralv resexg eqcomd eleq1d ralimia biimpri 3exp syl5com com3l ex
      biimpd syl6 com13 pm2.43i com14 3imp sylbi impcom ) EABDFGZCBHZECIZACDFGZ
      VAEJGZEBKZALZEMZDGZABNZOVBVDPZABDEQVEVFVJVKVBVFVJVEVDVBVFVJVEVDPZPZPVFVBV
      BVMVFVBVBVMPZVFVBRVCCKZVNBCEUBVBVJVOVLVBVJVIACNZVOVLPVIACBUCVEVPVOVDVEVCJ
      GZVPVOVDPZECJUDVPVGVCMZDGZACNZVQVRPVIVTACVGCGZVIVTWBVHVSDWBVSVHVGCEUAUEUF
      UMUGVQVOWAVDVQVOWAVDVDVQVOWAOACDVCQUHUISTUJUKUNSTULUOUPUQURUSUT $.
      $( [7-Nov-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.  $d G x $.
    $( Union of two projections of a cartesian product.  (Contributed by FL,
       7-Nov-2011.) $)
    unprj $p |-
    ( ( F e. X_ x e. B C /\ G e. X_ x e. ( A \ B ) C /\ B C_ A )
        -> ( F u. G ) e. X_ x e. A C ) $=
      ( wcel w3a cun wfn wral wi wa c0 wceq sylancl syl syl5com wn eqcoms unexg
      cixp cdif wss cvv cv cfv 3adant3 elixp2a 3simpa ancomd disjdif fnun undif
      cin biimpi eqcomd 3ad2ant3 fneq2d mpbird 3exp 3imp cdm fndm elndif notbid
      wb eleq2 ndmfv syl6bi syl2im r19.21aiv elixp2b uneq2 un0 eqtr eleq1 com12
      biimpd ral2imi impcom eldifn uneq1 uncom imp ralunb sylanbrc raleq imbi2d
      syl5ibr sylbi com3l wfun df-fn ax-17 simpl anim12i ineq12 syl6eq ad2ant2l
      ex fvun syl11anc eleq1d ralbid 3jca elixp2 sylibr ) EACDUBZGZFABCUCZDUBZG
      ZCBUDZHZEFIZUEGZXPBJZAUFZXPUGZDGZABKZHXPABDUBGXOXQXRYBXJXMXQXNEFXIXLUAUHX
      JXMXNXRXJECJZXMXNXRLZACDEUIZXMFXKJZYCYDLAXKDFUIZYFYCXNXRYFYCXNHZXRXPCXKIZ
      JZYHYCYFMCXKUOZNOYJYHYFYCYFYCXNUJUKCBULZCXKEFUMPYHBYIXPXNYFBYIOZYCXNYIBXN
      YIBOZCBUNZUPUQURUSUTVAQRVBXOYBXSEUGZXSFUGZIZDGZABKZXJXMXNYTXNXJXMYTXNYNXJ
      XMYTLZLZYOUUBBYIXJUUAYMXMYSAYIKZLXJXMUUCXJXMMYSACKZYSAXKKZUUCXMXJUUDXMYFX
      JUUDLYGYFYQNOZACKZXJUUDYFUUFACYFFVCZXKOZXSCGZXSXKGZSZUUFXKFVDXSCBVEUUIUUL
      XSUUHGZSZUUFUULUUNVGXKUUHXKUUHOUUKUUMXKUUHXSVHVFTXSFVIVJVKVLXJYPDGZACKUUG
      UUDLACDEVMUUOUUFYSACUUFUUOYSUUFYRYPNIZOZUUPYPOZUUOYSLZYQNYPVNYPVOUUQUURMY
      RYPOUUSYRUUPYPVPUUSYPYRYPYROUUOYSYPYRDVQVSTQPVRVTQRQWAXJXMUUEXJYCXMUUELYE
      YCYPNOZAXKKZXMUUEYCUUTAXKYCEVCZCOZUUKUUJSZUUTCEVDXSBCWBUVDUUTLCUVBCUVBOZU
      VDXSUVBGZSUUTUVEUUJUVFCUVBXSVHVFXSEVIVJTVKVLXMYQDGZAXKKUVAUUELAXKDFVMUVGU
      UTYSAXKUUTUVGYSUUTYRNYQIZOZUVHYQNIZOZUVGYSLZYPNYQWCNYQWDUVIUVKMYRUVJOZUVJ
      YQOZUVLYRUVHUVJVPYQVOUVMUVNMYRYQOUVLYRUVJYQVPUVLYQYRYQYROUVGYSYQYRDVQVSTQ
      PPVRVTQRQWEYSACXKWFWGXAYMYTUUCXMYSABYIWHWIWJTWKWLVBXJXMXNYBYTVGZXJYCXMXNU
      VOLZYEXMYFYCUVPLZYGYFFWMZUUIMZUVQFXKWNYCUVSUVPYCEWMZUVCMZUVSUVPLECWNUWAUV
      SXNUVOUWAUVSXNHZYAYSABUWBAWOUWBXTYRDUWBUVTUVRMZUVBUUHUOZNOZXTYROUWAUVSUWC
      XNUWAUVTUVSUVRUVTUVCWPUVRUUIWPWQUHUWAUVSUWEXNUVCUUIUWEUVTUVRUVCUUIMUWDYKN
      UVBCUUHXKWRYLWSWTUHXSEFXBXCXDXEVAWKVRWKQRVBUTXFABDXPXGXH $.
      $( [7-Nov-2011] $)
  $}

  ${
    $d A f h x $.  $d B f h x $.  $d B g x $.  $d C f h $.  $d C g $.
    $d G f h $.  $d G g $.  $d G x $.
    prl.1 $e |- A e. D $.
    $( Existence of a "prolongement" of a cartesian product.  Bourbaki E.II.34
       prop. 6.  (Contributed by FL, 7-Nov-2011.) $)
    prl $p |- ( ( A. x e. A C =/= (/) /\ B C_ A /\ G e. X_ x e. B C )
     -> E. f e. X_ x e. A C G C_ f ) $=
      ( vh cv cixp wcel wex c0 wne wss w3a wi a1d 19.23aiv cdif wral wrex difss
      elisseti ac9s n0 wa cres prjnpl eleq1 cla4egv sylc mpan sylbi imp 3adant3
      cun unprj ssun1 sseq2 rcla4ev mpan2 syl 3exp com34 com4t 3imp com12 mpcom
      ) IJZABCUAZDKZLZIMZDNOABUBZCBPZGACDKLZQZGFJZPZFABDKZUCZVPVQVOVRVPVQVOVPWB
      NOZVQVORZABDBEHUEUFWDVTWBLZFMWEFWBUGWFWEFWFVOVQVLBPZWFVOBCUDWGWFUHVTVLUIZ
      VMLZWIVOABVLDVTUJZWJVNWIIWHVMVKWHVMUKULUMUNSTUOUOUPUQVNVSWCRIVSVNWCVPVQVR
      VNWCRVRVNVPVQWCVRVNVQVPWCVRVNVQVPWCRZVRVNVQQGVKURZWBLZWKABCDGVKUSWMWCVPWM
      GWLPZWCGVKUTWAWNFWLWBVTWLGVAVBVCSVDVEVFVGVHVITVJ $.
      $( [7-Nov-2011] $)
  $}

  ${
    $d A g $.  $d B f g $.  $d C g $.  $d f g x $.  $d A f $.  $d A x $.
    $d B x $.  $d C f $.
    prl1.1 $e |- A e. D $.
    $( Existence of a "prolongement" of a cartesian product.  Bourbaki E.II.34
       prop. 6.  (Contributed by FL, 20-Nov-2011.) $)
    prl1 $p |- ( ( A. x e. A C =/= (/) /\ B C_ A )
     -> A. g e. X_ x e. B C E. f e. X_ x e. A C g C_ f ) $=
      ( c0 wne wral wss wa cv cixp wrex wcel prl 3expia r19.21aiv ) DIJABKZCBLZ
      MGNZFNLFABDOPZGACDOZUAUBUCUEQUDABCDEFUCHRST $.
      $( [20-Nov-2011] $)
  $}

  ${
    $d A f g $.  $d B f g u $.  $d C f g $.  $d f g x $.  $d A x $.  $d B x $.
    prl2.1 $e |- A e. D $.
    $( Existence of a "prolongement" of a cartesian product.  Bourbaki E.II.34
       prop. 6.  (Contributed by FL, 20-Nov-2011.) $)
    prl2 $p |- ( ( A. x e. A C =/= (/) /\ B C_ A )
     -> A. g e. X_ x e. B C E. f e. X_ x e. A C g = ( f |` B ) ) $=
      ( vu wral wss wa cv wceq wrex wcel cfv wfn wi com12 c0 wne cres cixp prl1
      elixp2a cdm fndm wfun fnfun fvres eleq2 funssfv eqcomd 3exp syl6bi eqcoms
      w3a com13 com24 3imp eqeq2 imbi2d syl5ibr mpcom ancld r19.21aiv ssid 3syl
      reximdvai syl ralimia wb ad2antlr fnssresb biimprcd syl5 eqfnfv3 syl11anc
      jctil imp rexbidva ralbidva mpbird ) DUAUBABJZCBKZLZGMZFMZCUCZNZFABDUDZOZ
      GACDUDZJCCKZIMZCPZWPWHQZWPWJQZNZLZICJZLZFWLOZGWNJZWGWHWIKZFWLOZGWNJXEABCD
      EFGHUEXGXDGWNWHWNPZWHCRZXGXDSZACDWHUFZXIWHUGZCNZXJCWHUHXMXFXCFWLWIWLPZXMX
      FXCSZXNWIBRZWIUIZXMXOSABDWIUFZBWIUJXQXMXFXCXQXMXFURZXBWOXSXAICXSWQWTWQXSW
      TWSWPWIQZNZWQXSWTSZWPCWIUKWQYBYAXSWRXTNZSXSWQYCXQXMXFWQYCSZXMXQXFYDSXMWQX
      FXQYCWQXFXQYCSSZSCXLCXLNWQWPXLPZYECXLWPULXQXFYFYCXQXFYFYCXQXFYFURXTWRWPWI
      WHUMUNUOUSUPUQUTTVATYAWTYCXSWSXTWRVBVCVDVETVFVGCVHVTUOVITVJVKVKVLVKWGWMXD
      GWNWGXHLZWKXCFWLYGXNLXIWJCRZWKXCVMXHXIWGXNXKVNYGXNYHXNXPYGYHXRWFXPYHSWEXH
      XPYHWFBCWIVOVPVNVQWAICCWHWJVRVSWBWCWD $.
      $( [20-Nov-2011] $)
  $}


  ${
    $d A a f x y $.  $d A f u v x $.  $d B a f y $.  $d B f u v $.  $d C f y $.
    $d D a f y $.  $d I a f x y $.  $d I f u v x $.
    $( A projection is a mapping from a cartesian product onto one of its
       restriction.  Bourbaki E.II.33 prop. 5.  (Contributed by FL,
       20-Nov-2011.) $)
    prjmapcp2 $p |- ( ( I C_ A /\ A e. C /\
      ( A. x e. A B e. D /\ A. x e. A B =/= (/) ) )
      -> ( X_ x e. A B prj I ) : X_ x e. A B -onto-> X_ x e. I B ) $=
      ( vy vf va vu vv wcel wral wa cixp cv wceq wrex wi cvv wss c0 cproj co wf
      wne w3a cfv wfo prjmapcp 3adant3r cres sseq2 anbi12d ixpeq1 eleq2d anbi1d
      raleq rexbidv2 ralbidv imbi12d pm3.2 adantl visset prl2 vtoclg com12 3imp
      syl6com copab simpr resexg ax-mp jctir reseq1 eqid fvopab4g simp3l adantr
      simp2 jca ixpexg syl 3adant3 isprj1 syl11anc eqcomd fveq1d eqeq1d biimpcd
      ssexg 3syl pm2.43i eqeq2d rexbidva mpbird dffo3 sylanbrc ) FBUAZBDLZCELZA
      BMZCUBUFZABMZNZUGZABCOZAFCOZXGFUCUDZUEZGPZHPZXIUHZQZHXGRZGXHMZXGXHXIUIWSW
      TXBXJXDABCDEFUJUKXFXPXKXLFULZQZHXGRZGXHMZWSWTXEXTWTWSXEXTSZFIPZUAZXAAYBMZ
      XCAYBMZNZXRHAYBCOZRZGXHMZSZSWSYASIBDYBBQZYCWSYJYAYBBFUMYKYFXEYIXTYKYDXBYE
      XDXAAYBBURXCAYBBURUNYKYHXSGXHYKXRXRHYGXGYKXLYGLXLXGLZXRYKYGXGXLAYBBCUOUPU
      QUSUTVAVAYFYCYEYCNZYIYEYCYMSYDYEYCVBVCAYBFCTHGIVDVEVIVFVGVHXFXOXSGXHXFXNX
      RHXGXFYLNZXMXQXKYNXMXQQZYNYLXQTLZNXLJPZXGLKPYQFULZQNJKVJZUHZXQQZYNYOSYNYL
      YPXFYLVKXLTLYPHVDXLFTVLVMVNJKXLYRXQXGTYSYQXLFVOYSVPVQYNUUAYOYNYTXMXQYNXLY
      SXIYNXIYSYNXGTLZFTLZXIYSQYNWTXBNZUUBXFUUDYLXFWTXBWSWTXEVTWSWTXBXDVRWAVSAB
      CDEWBWCXFUUCYLWSWTUUCXEFBDWKWDVSXGTJKFTWEWFWGWHWIWJWLWMWNWOUTWPHGXGXHXIWQ
      WR $.
      $( [20-Nov-2011] $)
  $}

  ${
    $d A f x z $.  $d B f y z $.  $d C f z $.  $d D y z $.  $d X z $.
    $d x y z $.
    dstr.1 $e |- ( y = ( f ` x ) -> C = D ) $.
    dstr.2 $e |- X = X_ x e. A B $.
    dstr.3 $e |- A e. E $.
    $( Distribution of union over intersection.  Bourbaki E.II.35 prop. 8.
       (Contributed by FL, 18-Jun-2011.) $)
    dstr $p |- U_ x e. A |^|_ y e. B C = |^|_ f e. X U_ x e. A D $=
      ( vz cv wcel wrex wral syl 3imtr4i wn ciin ciun eliun cixp eleq2i elixp2b
      wi cfv eleq2d rcla4v ralimi rexim sylbi com12 r19.21aiv ralbii sylibr cvv
      wceq wb visset eliin ax-mp rexbii ralnex crab rabn0 biimpri elisseti ac9s
      c0 wne wex wa wss ss2ixp ssrab2 mprg sseli syl6eleqr notbid elrab simprbi
      a1i jca eximi n0 df-rex sylib rexnal 3imtr3i sylbir con4i impbii eqriv )
      MACBDEUAZUBZGIACFUBZUAZMNZWQOZWTWSOZXAWTWPOZACPZXBAWTCWPUCZWTEOZBDQZACPZW
      TWROZGIQZXDXBXHWTFOZACPZGIQZXJXHXLGIGNZIOZXHXLXOXNACDUDZOZXHXLUGZIXPXNKUE
      XQANZXNUHZDOZACQZXRACDXNUFYBXGXKUGZACQXRYAYCACXFXKBXTDBNXTUSZEFWTJUIZUJUK
      XGXKACULRRUMUNUOXIXLGIAWTCFUCUPZUQXCXGACWTUROZXCXGUTMVAZBWTDEURVBVCVDZYGX
      BXJUTYHGWTIWRURVBVCZSUMXJXDXBXAXMXHXJXDXHXMXHTXGTZACQZXMTZXGACVEXFTZBDPZA
      CQZXLTZGIPZYLYMYPXKTZACQZGIPZYRYPYNBDVFZVKVLZACQZUUAYOUUCACUUCYOYNBDVGVHU
      KUUDACUUBUDZVKVLZUUAACUUBCHLVIVJXNUUEOZGVMXOYTVNZGVMUUFUUAUUGUUHGUUGXOYTU
      UGXNXPIUUEXPXNUUBDVOZUUEXPVOACACUUBDVPUUIXSCOYNBDVQWDVRVSKVTUUGXTUUBOZACQ
      YTACUUBXNUFUUJYSACUUJYAYSYNYSBXTDYDXFXKYEWAWBWCUKRWEWFGUUEWGYTGIWHSUMRYTY
      QGIXKACVEVDWIYOYKACXFBDWJUPXLGIWJWKWLWMYFYISYJXESWNWO $.
      $( [18-Jun-2011] $)

  $}


