$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Cartesian Products
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the projection symbol. $)
  $c pr $.

  $( Extend class notation to include the projection mapping. $)
  cpro $a class pr $.

  $( Declare the projection symbol. $)
  $c prj $.

  $( Extend class notation to include the projection mapping. $)
  cproj $a class prj $.

  ${
    $d x y z f g $.
    $( Definition of a projection.  Meaninful if ` x ` is a cartesian product
       and ` y ` is an index. $)
    df-pro $a |-
      pr = { <. <. x , y >. , z >. |
         z = { <. f , g >. | ( f e. x /\ g = ( f ` y ) ) } } $.
  $}

  ${
    $d x y z f g $.
    $( Definition of a projection.  Meaninful if ` x ` is a cartesian product
       and ` y ` is a set of indices. $)
    df-prj $a |-
      prj = { <. <. x , y >. , z >. |
         z = { <. f , g >. | ( f e. x /\ g = ( f |` y ) ) } } $.
  $}

  ${
    $d A f $.  $d B f $.  $d F f $.  $d f x $.
    $( A nuple is a function. $)
    elixp2a $p |- ( F e. X_ x e. A B -> F Fn A ) $=
      ( vf cixp wcel wfn cv wi wceq eleq1 fneq1 imbi12d cfv wral visset elixp
      pm3.26bi vtoclg pm2.43i ) DABCFZGZDBHZEIZUBGZUEBHZJUCUDJEDUBUEDKUFUCUGUDU
      EDUBLBUEDMNUFUGAIUEOCGABPABCUEEQRSTUA $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A f $.  $d B f $.  $d F f x $.
    $( The base class of the elements of a nuple. $)
    elixp2b $p |- ( F e. X_ x e. A B -> A. x e. A ( F ` x ) e. B ) $=
      ( vf cixp wcel cv cfv wral wi wceq eleq1 fveq1 eleq1d ralbidv imbi12d
      wfn visset elixp pm3.27bi vtoclg pm2.43i ) DABCFZGZAHZDIZCGZABJZEHZUDGZUF
      UJIZCGZABJZKUEUIKEDUDUJDLZUKUEUNUIUJDUDMUOUMUHABUOULUGCUFUJDNOPQUKUJBRUNA
      BCUJESTUAUBUC $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A x $.  $d C x $.  $d F x $.  $d I x $.
    bcelnu.1 $e |- ( x = I -> B = C ) $.
    $( The base class of an element of a nuple. $)
    bclelnu $p |- ( ( F e. X_ x e. A B /\ I e. A ) -> ( F ` I ) e. C ) $=
      ( cixp wcel cv cfv wral elixp2b wceq fveq2 eleq12d rcla4va ex mpan9 ) EAB
      CHIAJZEKZCIZABLZFBIZFEKZDIZABCEMUDUCUFUBUFAFBTFNUAUECDTFEOGPQRS $.
      $( [19-Jun-2011] $)
  $}


  ${
    $d I f g x y z $.  $d P f g x y z $.
    $( Definition of the coordinate mapping (or projection ) of index ` I ` .
       ` P ` is a cartesian product. $)
    ispr1 $p |- ( ( P e. Q /\ I e. J ) -> ( P pr I ) =
      { <. f , g >. | ( f e. P /\ g = ( f ` I ) ) } ) $=
      ( vx vy vz cvv wcel wa cv cfv wceq copab cpro co eleq2 anbi1d opabbidv
      fveq2 eqeq2d anbi2d copab2 cxp cres df-pro wrel cdm reloprab reldmoprab
      resid2 mp2an resoprab 3eqtr2i oprabval2g 3expia elisset anim12i opabex2g
      adantr sylc ) AJKZEJKZLCMZAKZDMZEVFNZOZLZCDPZJKZAEQRVLOZABKZEFKZLVDVEVMVN
      GHIAEJJVFGMZKZVHHMZVFNZOZLZCDPZVLQVGWALZCDPJVQAOZWBWDCDWEVRVGWAVQAVFSTUAV
      SEOZWDVKCDWFWAVJVGWFVTVIVHVSEVFUBUCUDUAQIMWCOZGHIUEZWHJJUFUGZVQJKVSJKLWGL
      GHIUEGHICDUHWHUIWHUJUIWIWHOWGGHIUKWGGHIULWHUMUNWGGHIJJUOUPUQURVOVDVPVEABU
      SEFUSUTVOVMVPCDAVIBVAVBVC $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f x $.  $d C f x y $.  $d I f x y $.  $d P f y $.
    prmapcp2.1 $e |- P = X_ x e. A B $.
    prmapcp2.2 $e |- ( x = I -> B = C ) $.
    $( A projection is a mapping from a cartesian product to a set of the
       family implied in the product. $)
    prmapcp2 $p |- ( ( P e. Q /\ I e. A )
      -> ( P pr I ) : P --> C ) $=
      ( vf vy wcel wa cv cfv wral cpro co wf cixp wi eleq2i bclelnu ex sylbi
      com12 r19.21aiv adantl wceq copab wb ispr1 fopab2g syl mpbid ) EFLZGBLZMZ
      GJNZOZDLZJEPZEDEGQRZSZUQVBUPUQVAJEUSELZUQVAVEUSABCTZLZUQVAUAEVFUSHUBVGUQV
      AABCDUSGIUCUDUEUFUGUHURVCVEKNUTUIMJKUJUIVBVDUKEFJKGBULJKEDUTVCUMUNUO $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d I f g $.  $d P f g $.
    $( A projection is a mapping whose domain is a cartesian product. $)
    prmapcp3 $p |- ( ( P e. Q /\ I e. A )
      -> ( P pr I ) Fn P ) $=
      ( vf vg wcel wa cpro co cv cfv wceq copab wfn ispr1 fvex eqid fnopab2
      fneq1 mpbiri syl ) BCGDAGHBDIJZEKZBGFKDUDLZMHEFNZMZUCBOZBCEFDAPUGUHUFBOEF
      BUEUFDUDQUFRSBUCUFTUAUB $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d F f g $.  $d I f g $.  $d P f g $.
    $( The projection of index ` I ` of the nuple ` F ` . $)
    valpr $p |- ( ( P e. Q /\ I e. J /\ F e. P )
      -> ( ( P pr I ) ` F ) = ( F ` I ) ) $=
      ( vf vg wcel w3a cpro co cfv cv wceq wa copab ispr1 3adant3 fveq1d cvv
      fvex fveq1 eqid fvopab4g mpan2 3ad2ant3 eqtrd ) ABHZDEHZCAHZIZCADJKZLCFMZ
      AHGMDUMLZNOFGPZLZDCLZUKCULUOUHUIULUONUJABFGDEQRSUJUHUPUQNZUIUJUQTHURDCUAF
      GCUNUQATUODUMCUBUOUCUDUEUFUG $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A x $.  $d x y $.
    $( The variable x is bound in ` X_ x e. A B ` . $)
    hbcp $p |- ( y e. X_ x e. A B -> A. x y e. X_ x e. A B ) $=
      ( cv wfn cfv wcel wral wa wal cixp ax-17 hbra1 hban visset elixp albii
      3imtr4i ) BEZCFZAETGDHZACIZJZUDAKTACDLHZUEAKUAUCAUAAMUBACNOACDTBPQZUEUDAU
      FRS $.
      $( [20-Jun-2011] $)
  $}


  ${
    $d A f x y $.  $d B y $.  $d F f g x y $.  $d P f g $.  $d Q f x y $.
    npincppr.1 $e |- P = X_ x e. A B $.
    $( A set of nuples is included in the cartesian product of the projections
       of the nuples.  Bourbaki E.II.32. $)
    npincppr $p |- ( ( F C_ P /\ P e. Q )
      -> F C_ X_ x e. A ( ( P pr x ) " F ) ) $=
      ( vf vy vg wss wcel wa cv cpro co cima cixp wral wi dfss3 eleq2i wfn cfv
      visset elixp simpll eleq1i sseq2i w3a ax-17 hbcp hbss wal hbel albii
      3imtr4i hb3an hban wceq wrex ssel eqcomi valpr 3exp com3r sylbi syl6com
      com4l imp 3adant3 a1d 3imp fveq2 eqeq1d rcla4ev ex sylcom com24 imp41 wb
      fvelimab prmapcp3 3ad2ant2 adantr biimpi 3ad2ant1 ad2antrr sylanc mpbird
      ralimdaa 3exp1 com13 pm2.43i imp31 com12 adantl sylanbrc ralimdvaa
      sylibr ) FDKZDELZMHNZABDANZOPZFQZRZLZHFSZFXGKXAXBXIXAXBXITZXAXCDLZHFSZXAX
      JTHFDUAXBXAXLXIXBXAXLXITXBXAMZXKXHHFXKXMXCFLZMZXHXKXCABCRZLZXOXHTZDXPXCGU
      BXQXCBUCZXDXCUDZCLZABSZMZXRABCXCHUEZUFYCXOXHXHXSXTXFLZABSZYCXOMABXFXCYDUF
      XSYBXOUGYCXOYFYBXOYFTXSXOYBYFXBXAXNYBYFTZXBXAXNYGTZTZXBXPELZXBYITDXPEGUHZ
      XAXBYJYHXAFXPKZXBYJYHTTDXPFGUIYLXBYJXNYGYLXBYJUJZXNMZYAYEABYMXNAYLXBYJAAI
      FXPINZFLAUKAIBCULZUMYJYJAUNXBXBAUNAIIXPEYPYOELAUKUOZYKXBYJAYKUPUQYQURXNAU
      KUSYNXDBLZMZYAYEYSYAMZYEJNZXEUDZXTUTZJFVAZYMXNYRYAUUDYMYAYRXNUUDYMYAYRXNU
      UDTYMYAYRUJXNXCXEUDZXTUTZUUDYMYAYRXNUUFTZYMYRUUGTZYAYLXBUUHYJYLXBUUHXNYLX
      BYRUUFYLXNXQXBYRUUFTTZFXPXCVBXQXKUUIXPDXCDXPGVCZUBXBYRXKUUFXBYRXKUUFDEXCX
      DBVDVEVFVGVHVIVJVKVLVMXNUUFUUDUUCUUFJXCFUUAXCUTUUBUUEXTUUAXCXEVNVOVPVQVRV
      EVSVTXEDUCZXAYEUUDWAYTJDFXTXEWBYSUUKYAYNYRUUKYMYRUUKTZXNXBYLUULYJXBYRUUKB
      DEXDWCVQWDWEVJWEYNXAYRYAYMXAXNYLXBXAYJYLXAXPDFUUJUIWFWGWEWHWIWJVQWKWLVGWM
      VGWNWOWPWQVJWRVQVGVGWPWSVQWMVGWNVJHFXGUAWT $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d F x $.  $d I x $.  $d J x $.
    repfuntw.1 $e |- I e. A $.
    repfuntw.2 $e |- J e. B $.
    $( Representation as a set of pairs of a function whose domain has two
       distinct elements. $)
    repfuntw $p |- ( I =/= J -> ( F Fn { I , J } <->
       F = { <. I , ( F ` I ) >. , <. J , ( F ` J ) >. } ) ) $=
      ( vx wne cpr wfn cfv cop wceq wa cv wral wcel wi wo elprg fveq2 eqeq12d
      csn cun cima cab cuni c0 cdm cin dmsnop ineq1 eqeq1 necom disjsn2 sylbi
      syl5bir syl ax-mp adantr imadisj sylibr uneq2 un0 syl6eq eqeq1d abbidv
      unieqd elisseti fvex fvsn df-fv eqtr3i syl6reqr imaundir eqcomi eqeq1i
      abbii unieqi a1i df-pr fveq1d 3eqtrd incom uncom imaeq1i jaoi syl6bi
      pm2.43i com12 r19.21aiv wb eqfnfv2 pm3.27 wfun funpr dmeqi dmun uneq12i
      eqtr4i 3eqtri jctir df-fn sylanc mpbird ineq12d eqtrd funsn pm3.2i jctil
      funun funeq biimpd eqcoms 3syl eqtr syl5com mpan2 impcom dmeq adantl
      sylanbrc impbida ) DEIZCDEJZKZCDDCLZMZEECLZMZJZNZYEYGOZYMHPZCLZYOYLLZNZHY
      FQZYNYRHYFYOYFRZYNYRYTYNYRSZYTYTYODNZYOENZTUUAYODEYFUAUUBUUAUUCUUBYRYHDYL
      LZNYNUUBYPYHYQUUDYODCUBYODYLUBUCYNYHYIUDZYKUDZUEZDUDZUFZYOUDZNZHUGZUHZDUU
      GLZUUDYNYHUUEUUHUFZUUFUUHUFZUEZUUJNZHUGZUHZUUMYNUUPUINZYHUUTNYNUUFUJZUUHU
      KZUINZUVAYEUVDYGUVBEUDZNZYEUVDSZEYJULZUVFUVCUVEUUHUKZNZUVGUVBUVEUUHUMUVJU
      VDUVIUINZYEUVCUVIUIUNYEEDIUVKDEUOEDUPUQZURUSUTVAUUFUUHVBVCUVAUUTUUOUUJNZH
      UGZUHZYHUVAUUSUVNUVAUURUVMHUVAUUQUUOUUJUVAUUQUUOUIUEUUOUUPUIUUOVDUUOVEVFV
      GVHVIDUUELYHUVODYHDAFVJZDCVKZVLHDUUEVMVNVOUSUUSUULUURUUKHUUQUUIUUJUUIUUQU
      UEUUFUUHVPVQVRVSVTVFUUMUUNNYNUUNUUMHDUUGVMVQWAYNDUUGYLUUGYLNYNYLUUGYIYKWB
      ZVQWAZWCWDURUUCYRYJEYLLZNYNUUCYPYJYQUVTYOECUBYOEYLUBUCYNYJUUGUVEUFZUUJNZH
      UGZUHZEUUGLZUVTYNYJUUFUVEUFZUUEUVEUFZUEZUUJNZHUGZUHZUUFUUEUEZUVEUFZUUJNZH
      UGZUHZUWDYNUWGUINZYJUWKNYNUUEUJZUVEUKZUINZUWQYEUWTYGUWRUUHNZYEUWTSZDYHULZ
      UXAUWSUVINZUXBUXAUWSUUHUVEUKZUVIUWRUUHUVEUMUUHUVEWEVFUXDUWTUVKYEUWSUVIUIU
      NUVLURUSUTVAUUEUVEVBVCUWQUWKUWFUUJNZHUGZUHZYJUWQUWJUXGUWQUWIUXFHUWQUWHUWF
      UUJUWQUWHUWFUIUEUWFUWGUIUWFVDUWFVEVFVGVHVIEUUFLYJUXHEYJEBGVJZECVKZVLHEUUF
      VMVNVOUSUWKUWPNYNUWJUWOUWIUWNHUWHUWMUUJUWMUWHUUFUUEUVEVPVQVRVSVTWAUWPUWDN
      YNUWOUWCUWNUWBHUWMUWAUUJUWLUUGUVEUUFUUEWFWGVRVSVTWAWDUWDUWENYNUWEUWDHEUUG
      VMVQWAYNEUUGYLUVSWCWDURWHWIWJWKWLYGYLYFKZYMYSWMYNHYFCYLWNYEYGWOYEUXKYGYEY
      LWPZYLUJZYFNZOUXKYEUXLUXNDEYHYJUVPUXIUVQUXJWQUXMUUGUJUWRUVBUEZYFYLUUGUVRW
      RUUEUUFWSUXOUUHUVEUEYFUWRUUHUVBUVEUXCUVHWTDEWBXAXBZXCYLYFXDVCVAXEXFYGCWPZ
      CUJZYFNZYEYMOCYFXDYMYEUXQYMYLUUGNZYEUXQSUVRYECUUGNZUXQYMUXTOYEUUEWPZUUFWP
      ZOZUWRUVBUKZUINZOUUGWPZUYAUXQSYEUYFUYDYEUYEUXEUIYEUWRUUHUVBUVEUXAYEUXCWAU
      VFYEUVHWAXGDEUPXHUYBUYCDYHUVPUVQXIEYJUXIUXJXIXJXKUUEUUFXLUYAUYGUXQUYGUXQS
      UUGCUUGCNUYGUXQUUGCXMXNXOWKXPCYLUUGXQXRXSXTYMUXSYEYMUXRUXMYFCYLYAUXPVFYBY
      CYD $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d A a b f x $.  $d B a b f $.  $d I a b f x $.  $d J a b f x $.
    $d M a b x $.  $d M x y $.  $d N a b x $.  $d N x y $.
    repcpwti.1 $e |- A = { I , J } $.
    repcpwti.2 $e |- B = if ( x = I , M , N ) $.
    repcpwti.3 $e |- I e. C $.
    repcpwti.4 $e |- J e. D $.
    $( A representation of a cartesian product with two indices. $)
    repcpwti $p |- ( I =/= J
                -> X_ x e. A B = { f
                    | E. a
                       e. M
                      E. b
                       e. N
                          f = { <. I , a >. , <. J , b >. } } ) $=
      ( vy wne cv wfn cfv wcel wral wa cab cop cpr wceq wrex cixp wi wsbc wb
      raleq ax-mp elisseti ralpr bitri csb cvv sbcel12g biimpd csbfvg ax-17
      cif iftrue syl5eq csbief eleq12i a1i wal neeq2 eqcoms wn df-ne eqcom
      notbii iffalse sylbi syl6com 19.21aiv csbieb sylib eleq12d fneq2i
      repfuntw biimpcd opeq2 preq1 syl adantr eqeq2d rexbidva rcla4ev ex preq2
      syl5com expcom com3r com34 syl6bi pm2.43a com4l com12 imp impcom fneq1
      fveq1 eleq1d ralbidva anbi12d imbi2d w3a fnprg sylibr 3exp com13 mpan
      imp31 wo visset elpr eleq2 eleq1a fvsn2a syl5 syl5bir eleq2i syl6ibr
      fveq2 sylibrd fvsn2b eleq1 necom pm3.27 syl6bir com3l mpcom jaoi eleq2s
      r19.21aiv jca syl5cbir r19.23aivv impbid abbidv df-ixp ) GHRZFSZBTZASZUUI
      UAZCUBZABUCZUDZFUEUUIGKSZUFZHLSZUFZUGZUHZLJUIZKIUIZFUEABCUJUUHUUOUVCFUUHU
      UOUVCUUOUUHUVCUUNUUJUUHUVCUKZUUNUUMAGULZUUMAHULZUDZUUJUVDUKZUUNUUMAGHUGZU
      CZUVGBUVIUHUUNUVJUMMUUMABUVIUNUOUUMAGHGDOUPZHEPUPZUQURUVEUVFUVHUVEAGUULUS
      ZAGCUSZUBZUVFUVHUKZGUTUBZUVEUVOUKUVKUVQUVEUVOAGUULCUTVAVBUOUVOGUUIUAZIUBZ
      UVPUVMUVRUVNIUVQUVMUVRUHUVKAGUTUUIVCUOAQGCIUVKQSZIUBAVDUUKGUHZUWAIJVEZICU
      WAIJVFZNVGVHVIUVFUVSUVHUVFAHUULUSZAHCUSZUBZUVSUVHUKHUTUBZUVFUWFUMUVLAHUUL
      CUTVAUOUUHUWFUVSUUJUVCUWFUUHUVSUUJUVCUKUKZUUHUWFHUUIUAZJUBZUUHUWHUKUUHUWD
      UWIUWEJUWDUWIUHZUUHUWGUWKUVLAHUTUUIVCUOVJUUHUUKHUHZCJUHZUKZAVKUWEJUHUUHUW
      NAUWLUUHGUUKRZUWMUWLUUHUWOUUHUWOUMHUUKHUUKGVLVMVBUWOGUUKUHZVNZUWMGUUKVOUW
      QUWBJCUWQUWAVNZUWBJUHZUWPUWAGUUKVPVQUWAIJVRZVSNVGVSVTWAAQHCJUVLUVTJUBAVDW
      BWCWDUWJUUHUUJUVSUVCUUHUUJUWJUVSUVCUKZUUJUUHUUIGUVRUFZHUWIUFZUGZUHZUWJUXA
      UKUUJUUIUVITZUUHUXEUKBUVIUUIMWEUUHUXFUXEDEUUIGHOPWFWGVSUWJUXEUXAUVSUUIUXB
      UUSUGZUHZLJUIZUVCUWJUXEUDUVSUXIUVCUVBUXIKUVRIUUPUVRUHZUVAUXHLJUXJUURJUBZU
      DUUTUXGUUIUXJUUTUXGUHZUXKUXJUUQUXBUHUXLUUPUVRGWHUUQUXBUUSWIWJWKWLWMWNWOUX
      HUXELUWIJUURUWIUHZUXGUXDUUIUXMUUSUXCUHUXGUXDUHUURUWIHWHUUSUXCUXBWPWJWLWNW
      QWRVTWSWTXAXBXCVSXDVSWJXEVSXFXDUVCUUHUUOUVAUUHUUOUKZKLIJUVAUXNUUHUUTBTZUU
      KUUTUAZCUBZABUCZUDZUKUUPIUBZUXKUDZUVAUUOUXSUUHUVAUUJUXOUUNUXRBUUIUUTXGUVA
      UUMUXQABUVAUUMUXQUMUUKBUBZUVAUULUXPCUUKUUIUUTXHXIWKXJXKXLUYAUUHUXSUYAUUHU
      DZUXOUXRUXTUXKUUHUXOUVQUXTUXKUUHUXOUKZUKUVKUXKUVQUXTUDZUYDUWGUXKUYEUYDUKU
      VLUUHUYEUWGUXKUDZUXOUUHUYEUYFUXOUUHUYEUYFXMUUTUVITUXOGHUUPUURUTIUTJXNBUVI
      UUTMWEXOXPXQXRXDXRXSUYCUXQABUYBUYCUXQUYCUXQUKZUUKUVIBUUKUVIUBUWAUWLXTUYGU
      UKGHAYAYBUWAUYGUWLUWAUYCGUUTUAZCUBZUXQUWAUYCUYHUWBUBZUYIUWAUWBIUHZUYCUYJU
      KUWCUYKUYJUYHIUBZUYCUWBIUYHYCUYAUUHUYLUYAUYHUUPUHZUYLUUHUXTUYMUYLUKUXKUUP
      IUYHYDWKGHUUPUURDEUTUTOPKYAZLYAZYEYFXEYGWJCUWBUYHNYHYIUWAUXPUYHCUUKGUUTYJ
      XIYKUWLUYCHUUTUAZCUBZUXQUWLUYCUYPUWBUBZUYQUYCUWLUYRUUHUYAUWLUYRUKZUYPUURU
      HZUUHUYAUYSUKZGHUUPUURDEUTUTOPUYNUYOYLUYTVUAUYAUWLUURUWBUBZUKZUKUUHUYTUYS
      VUCUYAUYTUYRVUBUWLUYPUURUWBYMXLXLUWLUUHUYAVUBUWLUUHUWOUYAVUBUKZUUKHGVLUWO
      UUKGRZVUDGUUKYNVUEUWRVUDUUKGVOUWRUWSVUDUWTUWSVUBUXKUYAUWBJUURYCUXTUXKYOYG
      WJVSVSYPYQYGYRXFXDCUWBUYPNYHYIUWLUXPUYPCUUKHUUTYJXIYKYSVSMYTXDUUAUUBWOUUC
      UUDXDUUEUUFABCFUUGVG $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d A a b f x $.  $d B a b f $.  $d F c d e f y $.  $d F e f x y $.
    $d I a b c d e f $.  $d I a b e f x $.  $d I c d e f y $.
    $d J a b c d e f $.  $d J a b e f x $.  $d J c d e f y $.
    $d M a b c d e f $.  $d M a b e f x $.  $d M c d e f y $.
    $d N a b c d e f $.  $d N a b e f x $.  $d N c d e f y $.
    cbcpcp.1 $e |- A = { I , J } $.
    cbcpcp.2 $e |- B = if ( x = I , M , N ) $.
    cbcpcp.3 $e |- F = ( a e. M , b e. N |-> { <. I , a >. , <. J , b >. } ) $.
    cbcpcp.4 $e |- I e. C $.
    cbcpcp.5 $e |- J e. D $.
    $( The canonical bijection between a cross product and a cartesian product
       (whose set of indices is composed of two different elements).  Bourbaki
       E.II.33 . $)
    cbcpcp $p |- ( I =/= J -> F : ( M X. N ) -1-1-onto-> X_ x e. A B ) $=
      ( vy vf ve vc vd wne cxp wfn crn cixp wceq cv cfv wi wral w3a wf1o cop
      cpr cvv wcel wa prex a1i rgen2 wb fnoprab2ga mpbid wrex cab rngop
      elisset ax-mp repcpwti eqcomd eqtrd wex elxp co opeq2 preq1 syl preq2
      ovmpt2g df-opr eqtr wo opex preq12b visset opth2 opeq12 ex com12 com3l
      imp opth1 wn df-ne pm2.21 sylbi adantr jaoi eqeq2 imbi1d imbi2d mpbird
      eqcoms 3exp eqeq1 fveq2 eqeq2d imbi12d 19.23aivv eqeq1d r19.21aivv 3jca
      dff1o6 sylibr ) GHUCZFIJUDZUEZFUFZABCUGZUHZAUIZFUJZRUIZFUJZUHZYCYEUHZUKZR
      XRULAXRULZUMXRYAFUNXQXSYBYJXQGKUIZUOZHLUIZUOZUPZUQURZLJULKIULZXSYQXQYPKLI
      JYPYKIURYMJURUSYLYNUTVAVBVAYQXSVCXQKLIJYOFOVDVAVEXQXTSUIZYOUHLJVFKIVFSVGZ
      YAXTYSUHXQKLSIJYOFOVHVAXQYAYSABCUQUQSGHIJKLMNGDURGUQURPGDVIVJZHEURHUQURQH
      EVIVJVKVLVMXQYIARXRXRYCXRURZYEXRURZUSXQYIUUAUUBXQYIUKZUUAYCTUIZYRUOZUHZUU
      DIURZYRJURZUSZUSZSVNTVNUUBUUCUKZTSYCIJVOUUJUUKTSUUFUUIUUKUUFUUIUUKUKUUIUU
      BXQUUEFUJZYFUHZUUEYEUHZUKZUKZUKZUKZUURUUFUUBUUIUUPUUBYEUAUIZUBUIZUOZUHZUU
      SIURZUUTJURZUSZUSZUBVNUAVNUUIUUPUKZUAUBYEIJVOUVFUVGUAUBUVBUVEUVGUVBUVEUVG
      UKUVEUUIXQUULUVAFUJZUHZUUEUVAUHZUKZUKZUKZUKZUVNUVBGUUDUOZHYRUOZUPZUQURZUV
      NUVOUVPUTUUIUVRUVEUVLUUGUUHUVRUVEUVLUKZUKUUGUUHUVRUVSUUGUUHUVRUMUUDYRFVPZ
      UVQUHZUVSKLUUDYRIJYOUVQFUVOYNUPZUQYKUUDUHYLUVOUHYOUWBUHYKUUDGVQYLUVOYNVRV
      SYMYRUHYNUVPUHUWBUVQUHYMYRHVQYNUVPUVOVTVSOWAUVTUULUHUWAUVSUKZUUDYRFWBUWCU
      ULUVTUULUVTUHZUWAUVSUWDUWAUSUULUVQUHZUVSUULUVTUVQWCUWEUVSUVEXQUVQUVHUHZUV
      JUKZUKZUKZUWIUWEGUUSUOZHUUTUOZUPZUQURZUWIUWJUWKUTUVEUWMUWHUVCUVDUWMUWHUKU
      VCUVDUWMUWHUVCUVDUWMUMUUSUUTFVPZUWLUHZUWHKLUUSUUTIJYOUWLFUWJYNUPZUQYKUUSU
      HYLUWJUHYOUWPUHYKUUSGVQYLUWJYNVRVSYMUUTUHYNUWKUHUWPUWLUHYMUUTHVQYNUWKUWJV
      TVSOWAUWNUVHUHUWOUWHUKZUUSUUTFWBUWQUVHUWNUVHUWNUHZUWOUWHUWRUWOUSUVHUWLUHZ
      UWHUVHUWNUWLWCUWSUWHXQUVQUWLUHZUVJUKZUKZUXBUWSUWTXQUVJUWTUVOUWJUHZUVPUWKU
      HZUSZUVOUWKUHZUVPUWJUHZUSZWDXQUVJUKZUVOUVPUWJUWKGUUDWEHYRWEGUUSWEHUUTWEWF
      UXEUXIUXHUXCUXDUXIUXCUUDUUSUHZUXDUXIUKGUUDGUUSTWGUAWGWHUXDUXJUXIUXDYRUUTU
      HZUXJUXIUKHYRHUUTSWGUBWGWHXQUXKUXJUVJUXKUXJUVJUKUKXQUXJUXKUVJUXJUXKUVJUUD
      YRUUSUUTWIWJWKVAWLVSWKVSWMUXFUXIUXGUXFGHUHZUXIGUUDHUUTYTWNXQUXLUVJXQUXLWO
      UXLUVJUKGHWPUXLUVJWQWRWKVSWSWTWRWKVAUWSUWGUXAXQUWSUWFUWTUVJUVHUWLUVQXAXBX
      CXDVSWJXEVJVSXFWMWKVJVAUWEUVLUWHUVEUWEUVKUWGXQUWEUVIUWFUVJUULUVQUVHXGXBXC
      XCXDVSWJXEVJVSXFWMWLVJVAUVBUVGUVMUVEUVBUUPUVLUUIUVBUUOUVKXQUVBUUMUVIUUNUV
      JUVBYFUVHUULYEUVAFXHXIYEUVAUUEXAXJXCXCXCXDWMXKWRWKVAUUFUUKUUQUUIUUFUUCUUP
      UUBUUFYIUUOXQUUFYGUUMYHUUNUUFYDUULYFYCUUEFXHXLYCUUEYEXGXJXCXCXCXDWMXKWRWM
      WKXMXNARXRYAFXOXP $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d I f g x y z $.  $d P f g x y z $.
    $( Definition of a projection. ` I ` is a set of indices. ` P ` is a
       cartesian product. $)
    isprj1 $p |- ( ( P e. Q /\ I e. J ) -> ( P prj I ) =
      { <. f , g >. | ( f e. P /\ g = ( f |` I ) ) } ) $=
      ( vx vy vz cvv wcel wa cv cres wceq copab cproj co eleq2 anbi1d opabbidv
      reseq2 eqeq2d anbi2d copab2 cxp df-prj wrel cdm reloprab reldmoprab
      resid2 mp2an resoprab 3eqtr2i oprabval2g 3expia elisset anim12i opabex2g
      adantr sylc ) AJKZEJKZLCMZAKZDMZVEENZOZLZCDPZJKZAEQRVKOZABKZEFKZLVCVDVLVM
      GHIAEJJVEGMZKZVGVEHMZNZOZLZCDPZVKQVFVTLZCDPJVPAOZWAWCCDWDVQVFVTVPAVESTUAV
      REOZWCVJCDWEVTVIVFWEVSVHVGVREVEUBUCUDUAQIMWBOZGHIUEZWGJJUFNZVPJKVRJKLWFLG
      HIUEGHICDUGWGUHWGUIUHWHWGOWFGHIUJWFGHIUKWGULUMWFGHIJJUNUOUPUQVNVCVOVDABUR
      EFURUSVNVLVOCDAVHBUTVAVB $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A x $.  $d I f g $.  $d P f g $.
    isprj2.1 $e |- P = X_ x e. A B $.
    $( Definition of a projection. ` I ` is a set of indices. ` P ` is a
       cartesian product. $)
    isprj2 $p |- ( ( I e. J /\ A e. C /\ A. x e. A B e. D )
      -> ( P prj I ) = { <. f , g >. | ( f e. P /\ g = ( f |` I ) ) } ) $=
      ( cvv wcel cproj co cv cres wceq wa copab wral w3a isprj1 cixp ixpexg
      syl5eqel 3adant1 3simp1 sylanc ) FLMZIJMZFINOGPZFMHPULIQRSGHTRUKBDMZCEMAB
      UAZUBFLGHIJUCUMUNUJUKUMUNSABCUDLFABCDEUEKUFUGUKUMUNUHUI $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f x y $.  $d B f y $.  $d C f $.  $d D f $.  $d I f x y $.
    $( A projection is a mapping from a cartesian product to one of its
       restriction. $)
    prjmapcp $p |- ( ( I C_ A /\ A e. C /\ A. x e. A B e. D )
      -> ( X_ x e. A B prj I ) : X_ x e. A B --> X_ x e. I B ) $=
      ( vf vy wss wcel wral w3a cv cres cixp cproj co wf wfn cfv wa cvv visset
      resexg ax-mp elixp wi elixp2 fnssres ex 3ad2ant2 sylbi com12 3ad2ant1
      imp pm2.27 fvres eqcomd eleq1d biimpd ralimia syl6 adantl ssralv syl5com
      syl5ib sylanbrc r19.21aiva wceq copab wb eqid isprj2 ssexg 3adant3
      syld3an1 fopab2g syl mpbid ) FBIZBDJZCEJABKZLZGMZFNZAFCOZJZGABCOZKZWHWFWH
      FPQZRZWCWGGWHWGWEFSZAMZWETZCJZAFKZWCWDWHJZUAAFCWEWDUBJZWEUBJGUCZWDFUBUDUE
      UFWCWQWLVTWAWQWLUGWBWQVTWLWQWRWDBSZWMWDTZCJZABKZLVTWLUGZABCWDUHWTWRXDXCWT
      VTWLBFWDUIUJUKULUMUNUOWCWQWPWCWTXCUAZWPWQVTWAXEWPUGWBXEXCXBAFKZUGZWPVTXCX
      GWPUGWTXCXGXFWPXCXFUPXBWOAFWMFJZXBWOXHXAWNCXHWNXAWMFWDUQURUSUTVAVBVCXBAFB
      VDVEUNABCWDWSUFVFUOVGVHWCWJWQHMWEVIUAGHVJVIZWIWKVKFUBJZWAWBXIVTABCDEWHGHF
      UBWHVLVMVTWAXJWBFBDVNVOVPGHWHWFWEWJVQVRVS $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f g x $.  $d A f x y $.  $d B f g $.  $d B f y $.  $d C f g x $.
    $d C f x y $.  $d D f g x $.  $d D f x y $.  $d E f g $.  $d E f y $.
    $d F f g $.
    cbicplem.1 $e |- F = ( f e. X_ x e. { A } B |-> ( f ` A ) ) $.
    cbicplem.2 $e |- ( x = A -> B = C ) $.
    $( Lemma for ~ cbicp . $)
    cbicplem $p |- ( ( A e. D /\ C e. E )
      -> F : X_ x e. { A } B -1-1-onto-> C ) $=
      ( vg vy wcel wa csn cixp wfn crn wceq cv cfv wi wral w3a wf1o cvv fvex
      a1i rgen wb fopab2ga mpbii wrex cab wex elixp2 3simpc visset pm3.26
      pm3.27 3jca impbii bitri anbi1i exbii df-rex syl5bb abbidv rnmpt wal
      cmpt eqid mptfn mpbi eqidd fvmptg eleq1a adantl mpd snidg adantr sylan
      eqcomd jca31 ex snex mptexg ax-mp fneq1 fveq1 eleq1d anbi12d eqeq2d
      cla4ev syl6 imp 19.23aiv impbid1 wsbc ralsng ax-17 fveq2 eleq12d sbciegf
      bitr2d anbi2d anbi1d exbidv bitrd 19.21aiv abeq2 sylibr 3eqtr4d fvopab2b
      cmpbvb im2anan9 eqeq1 imbi1d imbi2d eqeq2 elsn eqeq12d biimprd sylbi
      com12 r19.21aiv 3ad2ant3 jca elixp2a anim12i 3ad2ant1 eqfnfv syl mpbird
      3exp syl5bir syl6com mp2an pm2.43i r19.21aivv dff1o6 ) BEMZDGMZNZHABOZCPZ
      QZHRZDSZFTZHUAZKTZHUAZSZUUJUULSZUBZKUUFUCFUUFUCZUDUUFDHUEUUDUUGUUIUUQUUDB
      UUJUAZUFMZFUUFUCZUUGUUSFUUFUUSUUJUUFMZBUUJUGZUHUIUUTUUGUJUUDFUUFUURHIUKUH
      ULUUDLTZUURSZFUUFUMZLUNZUUJUUEQZATZUUJUAZCMZAUUEUCZNZUVDNZFUOZLUNZUUHDUUD
      UVEUVNLUUDUVAUVDNZFUOZUVNUVEUVQUVNUJUUDUVPUVMFUVAUVLUVDUVAUUJUFMZUVGUVKUD
      ZUVLAUUECUUJUPUVSUVLUVRUVGUVKUQUVLUVRUVGUVKUVRUVLFURUHUVGUVKUSUVGUVKUTVAV
      BVCVDVEUHUVDFUUFVFVGVHUUHUVFSUUDFLUUFUURHIVIUHUUDUVCDMZUVNUJZLVJDUVOSUUDU
      WALUUDUVTUVGUURDMZNZUVDNZFUOZUVNUUDUVTUWEUUDUVTAUUEUVCVKZUUEQZBUWFUAZDMZN
      ZUVCUWHSZNZUWEUUDUVTUWLUUDUVTNZUWGUWIUWKUWGUWMUVCUFMZAUUEUCUWGUWNAUUEUWNU
      VHUUEMZLURUHUIAUUEUVCUWFUWFVLZVMVNUHBUUEMZUVTUWIUUDUWQUVTNZUWHUVCSZUWIABU
      VCUVCUUEDUWFUVHBSZUVCVOUWPVPZUVTUWSUWIUBUWQUVCDUWHVQVRVSUUBUWQUUCBEVTWAZW
      BUWQUVTUWKUUDUWRUWHUVCUXAWCUXBWBWDWEUWDUWLFUWFUUEUFMUWFUFMBWFAUUEUVCUFWGW
      HUUJUWFSZUWCUWJUVDUWKUXCUVGUWGUWBUWIUUEUUJUWFWIUXCUURUWHDBUUJUWFWJZWKWLUX
      CUURUWHUVCUXDWMWLWNWOUWDUVTFUWCUVDUVTUWBUVDUVTUBUVGUURDUVCVQVRWPWQWRUUDUW
      DUVMFUUDUWCUVLUVDUUDUWBUVKUVGUUBUWBUVKUJUUCUUBUVKUVJABWSUWBUVJABEWTUVJUWB
      ABEUWBUWBAVJUBUUBUWBAXAUHUWTUVIUURCDUVHBUUJXBZJXCXDXEWAXFXGXHXIXJUVNLDXKX
      LXMUUDUUPFKUUFUUFUVAUULUUFMZNZUUDUUPUXGUUDUUPUBZUUSBUULUAZUFMZUXGUXGUXHUB
      ZUBUVBBUULUGUXGUUSUXJNUUKUURSZUUMUXISZNUXKUVAUUSUXLUXFUXJUXMUVAUUSUXLFUUF
      UURUFHIXNWEUXFUXJUXMKUUFUXIUFHFKUUFUURUXIHIBUUJUULWJXOXNWEXPUXLUXMUXKUXLU
      XKUXGUUDUURUUMSZUUOUBZUBZUBUXMUXLUXHUXPUXGUXLUUPUXOUUDUXLUUNUXNUUOUUKUURU
      UMXQXRXSXSUXMUXPUUDUURUXISZUUOUBZUBUXGUXMUXOUXRUUDUXMUXNUXQUUOUUMUXIUURXT
      XRXSUXGUUDUXQUUOUXGUUDUXQUDZUUOUUEUUESZUVIUVHUULUAZSZAUUEUCZNZUXSUXTUYCUX
      SUUEVOUXQUXGUYCUUDUXQUYBAUUEUWOUXQUYBUWOUWTUXQUYBUBABYAUWTUYBUXQUWTUVIUUR
      UYAUXIUXEUVHBUULXBYBYCYDYEYFYGYHUXSUVGUULUUEQZNZUUOUYDUJUXGUUDUYFUXQUVAUV
      GUXFUYEAUUECUUJYIAUUECUULYIYJYKAUUEUUEUUJUULYLYMYNYOYPYPWPYQYRYSYEYTVAFKU
      UFDHUUAXL $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A f g x $.  $d A f x y $.  $d B f g $.  $d B f y $.  $d C f x $.
    $d D f x $.  $d E f $.  $d F f y $.
    cbicp.1 $e |- F = ( g e. X_ x e. { A } B |-> ( g ` A ) ) $.
    cbicp.2 $e |- ( x = A -> B = C ) $.
    $( Canonical bijection between a cartesian product indexed by a singleton
       and the base class of the elements of the 1-uple.  Bourbaki E II.32 $)
    cbicp $p |- ( ( A e. D /\ C e. E )
      -> F : X_ x e. { A } B -1-1-onto-> C ) $=
      ( vf csn cixp cv cfv cmpt eqid fveq1 cmpbvb eqtri cbicplem ) ABCDEKGHHFAB
      LCMZBFNZOZPZKUBBKNZOZPIFKUBUDUGUEUEQBUCUFRSTJUA $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A f x z $.  $d B f y z $.  $d C f z $.  $d D y z $.  $d X z $.
    $d x y z $.
    dstr.1 $e |- ( y = ( f ` x ) -> C = D ) $.
    dstr.2 $e |- X = X_ x e. A B $.
    dstr.3 $e |- A e. E $.
    $( Distribution of union over intersection.  Bourbaki E.II.35 prop. 8. $)
    dstr $p |- U_ x e. A |^|_ y e. B C = |^|_ f e. X U_ x e. A D $=
      ( vz ciin ciun cv wcel wrex eliun wral cixp wi eleq2i cfv elixp2b wceq
      eleq2d rcla4v ralimi rexim syl sylbi com12 r19.21aiv ralbii sylibr cvv
      wb visset eliin ax-mp rexbii 3imtr4i wn ralnex crab c0 wne rabn0 biimpri
      elisseti ac9s wex wa wss ss2ixp ssrab2 a1i mprg sseli syl6eleqr notbid
      elrab pm3.27bi jca eximi n0 df-rex sylib rexnal 3imtr3i sylbir con4i
      impbii eqriv ) MACBDENZOZGIACFOZNZMPZWQQZWTWSQZXAWTWPQZACRZXBAWTCWPSZWTEQ
      ZBDTZACRZWTWRQZGITZXDXBXHWTFQZACRZGITZXJXHXLGIGPZIQZXHXLXOXNACDUAZQZXHXLU
      BZIXPXNKUCXQAPZXNUDZDQZACTZXRACDXNUEYBXGXKUBZACTXRYAYCACXFXKBXTDBPXTUFZEF
      WTJUGZUHUIXGXKACUJUKUKULUMUNXIXLGIAWTCFSUOZUPXCXGACWTUQQZXCXGURMUSZBWTDEU
      QUTVAVBZYGXBXJURYHGWTIWRUQUTVAZVCULXJXDXBXAXMXHXJXDXHXMXHVDXGVDZACTZXMVDZ
      XGACVEXFVDZBDRZACTZXLVDZGIRZYLYMYPXKVDZACTZGIRZYRYPYNBDVFZVGVHZACTZUUAYOU
      UCACUUCYOYNBDVIVJUIUUDACUUBUAZVGVHZUUAACUUBCHLVKVLXNUUEQZGVMXOYTVNZGVMUUF
      UUAUUGUUHGUUGXOYTUUGXNXPIUUEXPXNUUBDVOZUUEXPVOACACUUBDVPUUIXSCQYNBDVQVRVS
      VTKWAUUGXTUUBQZACTYTACUUBXNUEUUJYSACUUJYAYSYNYSBXTDYDXFXKYEWBWCWDUIUKWEWF
      GUUEWGYTGIWHVCULUKYTYQGIXKACVEVBWIYOYKACXFBDWJUOXLGIWJWKWLWMYFYIVCYJXEVCW
      NWO $.
      $( [18-Jun-2011] $)

  $}

