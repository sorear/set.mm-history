$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Cartesian Products
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the projection symbol. $)
  $c pr $.

  $( Extend class notation to include the projection mapping. $)
  cpro $a class pr $.

  $( Declare the projection symbol. $)
  $c prj $.

  $( Extend class notation to include the projection mapping. $)
  cproj $a class prj $.

  ${
    $d x y z f g $.
    $( Definition of a projection (also called a coordinate mapping).
       Meaninful if ` x ` is a cartesian product and ` y ` is an index.

       In what follows I will call nuple an element of a cartesian product.

       If ` X ` is a cartesian product, ` N ` a nuple of ` X ` , ` I ` an
       indice, ` ( ( X pr I ) `` N ) ` is the ` I ` th coordinate of the nuple
       ` N ` .

       Suppose the set of indices is ` { 1 , 2 } ` and ` X ` is the cartesian
       product
       ` { { <. 1 , a >. , <. 2 , u >. } , { <. 1 , a >. , <. 2 , v >. } } `
       then ` ( ( X pr 1 ) `` { <. 1 , a >. , <. 2 , u >. } ) = a ` and
       ` ( ( X pr 2 ) `` { <. 1 , a >. , <. 2 , u >. } ) = u ` . $)
    df-pro $a |-
      pr = { <. <. x , y >. , z >. |
         z = { <. f , g >. | ( f e. x /\ g = ( f ` y ) ) } } $.
  $}

  ${
    $d x y z f g $.
    $( Definition of a projection.  Meaninful if ` x ` is a cartesian product
       and ` y ` is a set of indices.  Suppose
       ` X = { { <. 1 , a >. , <. 2 , u >. } , { <. 1 , a >. , <. 2 , v >. } }
       ` then ` ( X prj 1 ) = { a } ` and ` ( X prj 2 ) = { u , v } ` . $)
    df-prj $a |-
      prj = { <. <. x , y >. , z >. |
         z = { <. f , g >. | ( f e. x /\ g = ( f |` y ) ) } } $.
  $}

  ${
    $d A f $.  $d B f $.  $d F f $.  $d f x $.
    $( A nuple is a function.  (Contributed by FL, 6-Jun-2011.) $)
    elixp2a $p |- ( F e. X_ x e. A B -> F Fn A ) $=
      ( vf cixp wcel wfn cv wceq eleq1 fneq1 imbi12d cfv wral vex elixp simplbi
      wi vtoclg pm2.43i ) DABCFZGZDBHZEIZUBGZUEBHZSUCUDSEDUBUEDJUFUCUGUDUEDUBKB
      UEDLMUFUGAIUENCGABOABCUEEPQRTUA $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A f $.  $d B f $.  $d F f x $.
    $( The base class of the elements of a nuple.  (Contributed by FL,
       6-Jun-2011.) $)
    elixp2b $p |- ( F e. X_ x e. A B -> A. x e. A ( F ` x ) e. B ) $=
      ( vf cixp wcel cv cfv wral wi wceq eleq1 fveq1 eleq1d ralbidv imbi12d wfn
      vex elixp simprbi vtoclg pm2.43i ) DABCFZGZAHZDIZCGZABJZEHZUDGZUFUJIZCGZA
      BJZKUEUIKEDUDUJDLZUKUEUNUIUJDUDMUOUMUHABUOULUGCUFUJDNOPQUKUJBRUNABCUJESTU
      AUBUC $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A x $.  $d C x $.  $d F x $.  $d I x $.
    bcelnu.1 $e |- ( x = I -> B = C ) $.
    $( The base class of an element of a nuple.  (Contributed by FL,
       19-Jun-2011.) $)
    bclelnu $p |- ( ( F e. X_ x e. A B /\ I e. A ) -> ( F ` I ) e. C ) $=
      ( cixp wcel cv cfv wral elixp2b wceq fveq2 eleq12d rcla4va ex mpan9 ) EAB
      CHIAJZEKZCIZABLZFBIZFEKZDIZABCEMUDUCUFUBUFAFBTFNUAUECDTFEOGPQRS $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d I f g x y z $.  $d P f g x y z $.
    $( Definition of the coordinate mapping (or projection ) of index ` I ` .
       ` P ` is a cartesian product.  (Contributed by FL, 19-Jun-2011.) $)
    ispr1 $p |- ( ( P e. Q /\ I e. J ) -> ( P pr I ) =
      { <. f , g >. | ( f e. P /\ g = ( f ` I ) ) } ) $=
      ( vx vy vz wcel wa cvv cv cfv wceq copab cpro elex opabbidv copab2 adantr
      co anim12i opabex2g eleq2 anbi1d fveq2 eqeq2d anbi2d cxp cres df-pro wrel
      reloprab reldmoprab resid2 mp2an resoprab 3eqtr2i oprabval2g 3expia sylc
      cdm ) ABJZEFJZKALJZELJZKCMZAJZDMZEVHNZOZKZCDPZLJZAEQUBVNOZVDVFVEVGABREFRU
      CVDVOVECDAVKBUDUAVFVGVOVPGHIAELLVHGMZJZVJHMZVHNZOZKZCDPZVNQVIWAKZCDPLVQAO
      ZWBWDCDWEVRVIWAVQAVHUEUFSVSEOZWDVMCDWFWAVLVIWFVTVKVJVSEVHUGUHUISQIMWCOZGH
      ITZWHLLUJUKZVQLJVSLJKWGKGHITGHICDULWHUMWHVCUMWIWHOWGGHIUNWGGHIUOWHUPUQWGG
      HILLURUSUTVAVB $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f x $.  $d C f x y $.  $d I f x y $.  $d P f y $.
    prmapcp2.1 $e |- P = X_ x e. A B $.
    prmapcp2.2 $e |- ( x = I -> B = C ) $.
    $( A projection is a mapping from a cartesian product to an element of the
       family implied in the product.  Bourbaki E.II.34 cor. 1.  (Contributed
       by FL, 19-Jun-2011.) $)
    prmapcp2 $p |- ( ( P e. Q /\ I e. A )
      -> ( P pr I ) : P --> C ) $=
      ( vf vy wcel wa cv cfv wral cpro co wf wceq wi eleq2i bclelnu sylbi com12
      cixp ex ralrimiv adantl copab wb ispr1 fopab2g syl mpbid ) EFLZGBLZMZGJNZ
      OZDLZJEPZEDEGQRZSZUQVBUPUQVAJEUSELZUQVAVEUSABCUFZLZUQVAUAEVFUSHUBVGUQVAAB
      CDUSGIUCUGUDUEUHUIURVCVEKNUTTMJKUJTVBVDUKEFJKGBULJKEDUTVCUMUNUO $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d I f g $.  $d P f g $.
    $( A projection is a mapping whose domain is a cartesian product.
       (Contributed by FL, 20-Jun-2011.) $)
    prmapcp3 $p |- ( ( P e. Q /\ I e. A )
      -> ( P pr I ) Fn P ) $=
      ( vf vg wcel wa cpro co cfv wceq copab wfn ispr1 fvex eqid fnopab2 fneq1
      cv mpbiri syl ) BCGDAGHBDIJZETZBGFTDUDKZLHEFMZLZUCBNZBCEFDAOUGUHUFBNEFBUE
      UFDUDPUFQRBUCUFSUAUB $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d F f g $.  $d I f g $.  $d P f g $.
    $( The ` I ` th coordinate of the nuple ` F ` .  (Contributed by FL,
       19-Jun-2011.) $)
    valpr $p |- ( ( P e. Q /\ I e. J /\ F e. P )
      -> ( ( P pr I ) ` F ) = ( F ` I ) ) $=
      ( vf vg wcel w3a cpro co cfv cv wceq wa copab ispr1 3adant3 fveq1d cvv
      fvex fveq1 eqid fvopab4g mpan2 3ad2ant3 eqtrd ) ABHZDEHZCAHZIZCADJKZLCFMZ
      AHGMDUMLZNOFGPZLZDCLZUKCULUOUHUIULUONUJABFGDEQRSUJUHUPUQNZUIUJUQTHURDCUAF
      GCUNUQATUODUMCUBUOUCUDUEUFUG $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A x $.  $d x y $.
    $( The variable x is bound in ` X_ x e. A B ` .  (Contributed by FL,
       20-Jun-2011.) $)
    hbcp $p |- ( y e. X_ x e. A B -> A. x y e. X_ x e. A B ) $=
      ( cv cixp wcel wfn cfv wral wa vex elixp ax-17 hbra1 hban hbxfrbi ) BEZAC
      DFGRCHZAERIDGZACJZKAACDRBLMSUAASANTACOPQ $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d A f x y $.  $d B y $.  $d F f g x y $.  $d P f g $.  $d Q f x y $.
    npincppr.1 $e |- P = X_ x e. A B $.
    $( A set of nuples is included in the cartesian product of the projections
       of the nuples.  Bourbaki E.II.32.  (Contributed by FL, 20-Jun-2011.) $)
    npincppr $p |- ( ( F C_ P /\ P e. Q )
      -> F C_ X_ x e. A ( ( P pr x ) " F ) ) $=
      ( vf vy vg wss wcel wa cv wral wi cfv sylbi imp ex cpro cima dfss3 eleq2i
      co cixp wfn vex elixp simpll eleq1i sseq2i w3a ax-17 hbcp hbss hbel hb3an
      hbxfrbi hban wceq wrex ssel eqcomi valpr 3exp com3r syl6com com4l 3adant3
      a1d 3imp fveq2 eqeq1d rcla4ev sylcom com24 imp41 prmapcp3 3ad2ant2 adantr
      wb biimpi 3ad2ant1 ad2antrr fvelimab syl2anc ralimdaa 3exp1 com13 pm2.43i
      mpbird imp31 com12 adantl sylanbrc ralimdva sylibr ) FDKZDELZMHNZABDANZUA
      UEZFUBZUFZLZHFOZFXEKWSWTXGWSWTXGPZWSXADLZHFOZWSXHPHFDUCWTWSXJXGWTWSXJXGPW
      TWSMZXIXFHFXIXKXAFLZMZXFXIXAABCUFZLZXMXFPZDXNXAGUDXOXABUGZXBXAQZCLZABOZMZ
      XPABCXAHUHZUIYAXMXFYAXMMXQXRXDLZABOZXFXQXTXMUJYAXMYDXTXMYDPXQXMXTYDWTWSXL
      XTYDPZWTWSXLYEPZPZWTXNELZWTYGPDXNEGUKZWSWTYHYFWSFXNKZWTYHYFPPDXNFGULYJWTY
      HXLYEYJWTYHUMZXLMZXSYCABYKXLAYJWTYHAAIFXNINZFLAUNAIBCUOZUPWTYHAYIAIIXNEYN
      YMELAUNUQZUSYOURXLAUNUTYLXBBLZMZXSYCYQXSMZYCJNZXCQZXRVAZJFVBZYKXLYPXSUUBY
      KXSYPXLUUBYKXSYPXLUUBPYKXSYPUMXLXAXCQZXRVAZUUBYKXSYPXLUUDPZYKYPUUEPZXSYJW
      TUUFYHYJWTUUFXLYJWTYPUUDYJXLXOWTYPUUDPPZFXNXAVCXOXIUUGXNDXADXNGVDZUDWTYPX
      IUUDWTYPXIUUDDEXAXBBVEVFVGRVHVISVJVKVLXLUUDUUBUUAUUDJXAFYSXAVAYTUUCXRYSXA
      XCVMVNVOTVPVFVQVRYRXCDUGZWSYCUUBWBYQUUIXSYLYPUUIYKYPUUIPZXLWTYJUUJYHWTYPU
      UIBDEXBVSTVTWASWAYLWSYPXSYKWSXLYJWTWSYHYJWSXNDFUUHULWCWDWAWEJDFXRXCWFWGWL
      TWHWIRWJRWKWMWNWOSABXDXAYBUIWPTRRWNWQTWJRWKSHFXEUCWR $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d F x $.  $d I x $.  $d J x $.
    repfuntw.1 $e |- I e. A $.
    repfuntw.2 $e |- J e. B $.
    $( Representation as a set of pairs of a function whose domain has two
       distinct elements.  (Contributed by FL, 26-Jun-2011.) $)
    repfuntw $p |- ( I =/= J -> ( F Fn { I , J } <->
       F = { <. I , ( F ` I ) >. , <. J , ( F ` J ) >. } ) ) $=
      ( vx cfv wceq wa wi csn cun cima cab cuni c0 cdm a1i wne cpr wfn cop wral
      cv wcel wo elprg cin dmsnop ineq1 necom disjsn2 sylbi eqeq1 syl5ibr ax-mp
      syl adantr imadisj sylibr uneq2 un0 syl6eq eqeq1d abbidv unieqd fvex fvsn
      elexi df-fv eqtr3i syl6reqr eqcomi eqeq1i abbii unieqi df-pr fveq1d fveq2
      imaundir 3eqtrd eqeq12d incom uncom imaeq1i syl6bi pm2.43i com12 ralrimiv
      jaoi wb simpr wfun funpr dmeqi dmun uneq12i eqtr4i 3eqtri eqfnfv2 syl2anc
      jctir df-fn mpbird eqtr ineq12d eqtrd funsn pm3.2i jctil funun funeq 3syl
      biimpd eqcoms syl5com mpan2 impcom dmeq adantl sylanbrc impbida ) DEUAZCD
      EUBZUCZCDDCIZUDZEECIZUDZUBZJZYEYGKZYMHUFZCIZYOYLIZJZHYFUEZYNYRHYFYOYFUGZY
      NYRYTYNYRLZYTYTYODJZYOEJZUHUUAYODEYFUIUUBUUAUUCYNYRUUBYHDYLIZJYNYHYIMZYKM
      ZNZDMZOZYOMZJZHPZQZDUUGIZUUDYNYHUUEUUHOZUUFUUHOZNZUUJJZHPZQZUUMYNUUPRJZYH
      UUTJYNUUFSZUUHUJZRJZUVAYEUVDYGUVBEMZJZYEUVDLZEYJUKZUVFUVCUVEUUHUJZJZUVGUV
      BUVEUUHULYEUVDUVJUVIRJZYEEDUAUVKDEUMEDUNUOZUVCUVIRUPUQUSURUTUUFUUHVAVBUVA
      UUTUUOUUJJZHPZQZYHUVAUUSUVNUVAUURUVMHUVAUUQUUOUUJUVAUUQUUORNUUOUUPRUUOVCU
      UOVDVEVFVGVHDUUEIYHUVODYHDAFVKZDCVIVJHDUUEVLVMVNUSUUSUULUURUUKHUUQUUIUUJU
      UIUUQUUEUUFUUHWBVOVPVQVRVEUUMUUNJYNUUNUUMHDUUGVLVOTYNDUUGYLUUGYLJYNYLUUGY
      IYKVSZVOTZVTWCUUBYPYHYQUUDYODCWAYODYLWAWDUQYNYRUUCYJEYLIZJYNYJUUGUVEOZUUJ
      JZHPZQZEUUGIZUVSYNYJUUFUVEOZUUEUVEOZNZUUJJZHPZQZUUFUUENZUVEOZUUJJZHPZQZUW
      CYNUWFRJZYJUWJJYNUUESZUVEUJZRJZUWPYEUWSYGUWQUUHJZYEUWSLZDYHUKZUWTUWRUVIJZ
      UXAUWTUWRUUHUVEUJZUVIUWQUUHUVEULUUHUVEWEVEYEUWSUXCUVKUVLUWRUVIRUPUQUSURUT
      UUEUVEVAVBUWPUWJUWEUUJJZHPZQZYJUWPUWIUXFUWPUWHUXEHUWPUWGUWEUUJUWPUWGUWERN
      UWEUWFRUWEVCUWEVDVEVFVGVHEUUFIYJUXGEYJEBGVKZECVIVJHEUUFVLVMVNUSUWJUWOJYNU
      WIUWNUWHUWMHUWGUWLUUJUWLUWGUUFUUEUVEWBVOVPVQVRTUWOUWCJYNUWNUWBUWMUWAHUWLU
      VTUUJUWKUUGUVEUUFUUEWFWGVPVQVRTWCUWCUWDJYNUWDUWCHEUUGVLVOTYNEUUGYLUVRVTWC
      UUCYPYJYQUVSYOECWAYOEYLWAWDUQWLWHWIWJWKYNYGYLYFUCZYMYSWMYEYGWNYEUXIYGYEYL
      WOZYLSZYFJZKUXIYEUXJUXLDEYHYJUVPUXHWPUXKUUGSUWQUVBNZYFYLUUGUVQWQUUEUUFWRU
      XMUUHUVENYFUWQUUHUVBUVEUXBUVHWSDEVSWTXAZXDYLYFXEVBUTHYFCYLXBXCXFYEYMKCWOZ
      CSZYFJZYGYMYEUXOYMYLUUGJZYEUXOLUVQYMUXRKCUUGJZYEUXOCYLUUGXGYEUUEWOZUUFWOZ
      KZUWQUVBUJZRJZKUUGWOZUXSUXOLYEUYDUYBYEUYCUXDRYEUWQUUHUVBUVEUWTYEUXBTUVFYE
      UVHTXHDEUNXIUXTUYADYHUVPXJEYJUXHXJXKXLUUEUUFXMUXSUYEUXOUYEUXOLUUGCUUGCJUY
      EUXOUUGCXNXPXQWJXOXRXSXTYMUXQYEYMUXPUXKYFCYLYAUXNVEYBCYFXEYCYD $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d x y M $.  $d x A f a b $.  $d x f I a b $.  $d x f J a b $.  $d x y N $.
    $d B f a b $.  $d M a b $.  $d N a b $.
    repcpwti.1 $e |- A = { I , J } $.
    repcpwti.2 $e |- B = if ( x = I , M , N ) $.
    repcpwti.3 $e |- I e. C $.
    repcpwti.4 $e |- J e. D $.
    $( A representation of a cartesian product with two indices.  (Contributed
       by FL, 26-Jun-2011.) $)
    repcpwti $p |- ( I =/= J
                -> X_ x e. A B = { f
                    | E. a
                       e. M
                      E. b
                       e. N
                          f = { <. I , a >. , <. J , b >. } } ) $=
      ( wcel wceq wi cvv vy wne cixp cv wfn cfv wral wa cab cop cpr wrex df-ixp
      wsbc raleqi elexi ralpr bitri csb sbcel12g biimpd ax-mp csbfvg cif iftrue
      ax-17 syl5eq csbief eleq12i wb a1i wal neeq2 eqcoms wn df-ne eqcom notbii
      iffalse sylbi syl6com alrimiv csbieb sylib eleq12d repfuntw biimpcd opeq2
      fneq2i preq2d eqeq2d rcla4ev preq1d adantr rexbidva ex expcom com3r com34
      syl5com syl6bi pm2.43a com4l com12 syl imp impcom fnprg 3exp com3l sylibr
      w3a 3imp com13 mpan imp31 wo elpr fvsn2a eleq1a syl5 eleq2 syl5ibr eleq2i
      syl6ibr fveq2 eleq1d sylibrd fvsn2b necom simpr syl6bir eleq1 imbi2d jaoi
      vex mpcom eleq2s ralrimiv jca fneq1 ralbidva anbi12d syl5ibrcom rexlimivv
      fveq1 impbid abbidv ) GHUBZABCUCFUDZBUEZAUDZUUJUFZCQZABUGZUHZFUIUUJGKUDZU
      JZHLUDZUJZUKZRZLJULZKIULZFUIABCFUMUUIUUPUVDFUUIUUPUVDUUPUUIUVDUUOUUKUUIUV
      DSZUUOUUNAGUNZUUNAHUNZUHZUUKUVESZUUOUUNAGHUKZUGUVHUUNABUVJMUOUUNAGHGDOUPZ
      HEPUPZUQURUVFUVGUVIUVFAGUUMUSZAGCUSZQZUVGUVISZGTQZUVFUVOSUVKUVQUVFUVOAGUU
      MCTUTVAVBUVOGUUJUFZIQZUVPUVMUVRUVNIUVQUVMUVRRUVKAGTUUJVCVBAUAGCIUVKUAUDZI
      QAVFUULGRZCUWAIJVDZINUWAIJVEZVGVHVIUVGUVSUVIUVGAHUUMUSZAHCUSZQZUVSUVISHTQ
      ZUVGUWFVJUVLAHUUMCTUTVBUUIUWFUVSUUKUVDUWFUUIUVSUUKUVDSSZUUIUWFHUUJUFZJQZU
      UIUWHSUUIUWDUWIUWEJUWDUWIRZUUIUWGUWKUVLAHTUUJVCVBVKUUIUULHRZCJRZSZAVLUWEJ
      RUUIUWNAUWLUUIGUULUBZUWMUWLUUIUWOUUIUWOVJHUULHUULGVMVNVAUWOGUULRZVOZUWMGU
      ULVPUWQCUWBJNUWQUWAVOZUWBJRZUWPUWAGUULVQVRUWAIJVSZVTVGVTWAWBAUAHCJUVLUVTJ
      QAVFWCWDWEUWJUUIUUKUVSUVDUUIUUKUWJUVSUVDSZUUKUUIUUJGUVRUJZHUWIUJZUKZRZUWJ
      UXASUUKUUJUVJUEZUUIUXESBUVJUUJMWIUUIUXFUXEDEUUJGHOPWFWGVTUWJUXEUXAUWJUXEU
      HUUJUXBUUTUKZRZLJULZUVSUVDUXHUXELUWIJUUSUWIRZUXGUXDUUJUXJUUTUXCUXBUUSUWIH
      WHWJWKWLUVSUXIUVDUVCUXIKUVRIUUQUVRRZUVBUXHLJUXKUUSJQZUHUVAUXGUUJUXKUVAUXG
      RUXLUXKUURUXBUUTUUQUVRGWHWMWNWKWOWLWPWTWQWAWRWSXAXBXCVTXDVTXEXFVTXGXDUVDU
      UIUUPUVBUUIUUPSZKLIJUUQIQZUXLUHZUXMUVBUUIUVABUEZUULUVAUFZCQZABUGZUHZSUXOU
      UIUXTUXOUUIUHZUXPUXSUXNUXLUUIUXPUVQUXNUXLUUIUXPSZSUVKUXLUVQUXNUHZUYBUWGUX
      LUYCUYBSUVLUUIUYCUWGUXLUHZUXPUUIUYCUYDUXPUUIUYCUYDXLUVAUVJUEZUXPUUIUYCUYD
      UYEUYCUUIUYDUYESZUVQUUIUYFSUXNUYDUVQUUIUYEUWGUVQUUIUYESZSUXLUVQUWGUYGUUIU
      VQUWGUYEUUIUVQUWGUYEGHUUQUUSTTXHXIXJXDWNXJWNXDXMBUVJUVAMWIXKXIXNXOXDXOXPU
      YAUXRABUULBQZUYAUXRUYAUXRSZUULUVJBUULUVJQUWAUWLXQUYIUULGHAYPXRUWAUYIUWLUW
      AUYAGUVAUFZCQZUXRUWAUYAUYJUWBQZUYKUWAUWBIRZUYAUYLSUWCUYAUYLUYMUYJIQZUXOUU
      IUYNUUIUYJUUQRZUXOUYNGHUUQUUSDETTOPKYPZLYPZXSUXNUYOUYNSUXLUUQIUYJXTWNYAXF
      UWBIUYJYBYCXECUWBUYJNYDYEUWAUXQUYJCUULGUVAYFYGYHUWLUYAHUVAUFZCQZUXRUWLUYA
      UYRUWBQZUYSUYAUWLUYTUUIUXOUWLUYTSZUYRUUSRZUUIUXOVUASZGHUUQUUSDETTOPUYPUYQ
      YIUUIVUCVUBUXOUWLUUSUWBQZSZSUWLUUIUXOVUDUWLUUIUWOUXOVUDSZUULHGVMUWOUULGUB
      ZVUFGUULYJVUGUWRVUFUULGVPUWRUWSVUFUWTUXOVUDUWSUXLUXNUXLYKUWBJUUSYBYCXEVTV
      TYLXJVUBVUAVUEUXOVUBUYTVUDUWLUYRUUSUWBYMYNYNYCYQXGXDCUWBUYRNYDYEUWLUXQUYR
      CUULHUVAYFYGYHYOVTMYRXDYSYTWPUVBUUPUXTUUIUVBUUKUXPUUOUXSBUUJUVAUUAUVBUUNU
      XRABUVBUUNUXRVJUYHUVBUUMUXQCUULUUJUVAUUFYGWNUUBUUCYNUUDUUEXDUUGUUHVG $.
      $( [26-Jun-2011] $)
  $}

  ${
    $( Warning: a b x distincts seem curious. $)
    $d A a b f x $.  $d B a b f $.  $d F c d e f y $.  $d F e f x y $.
    $d I a b c d e f $.  $d I a b e f x $.  $d I c d e f y $.
    $d J a b c d e f $.  $d J a b e f x $.  $d J c d e f y $.
    $d M a b c d e f $.  $d M a b e f x $.  $d M c d e f y $.
    $d N a b c d e f $.  $d N a b e f x $.  $d N c d e f y $.
    cbcpcp.1 $e |- A = { I , J } $.
    cbcpcp.2 $e |- B = if ( x = I , M , N ) $.
    cbcpcp.3 $e |- F = ( a e. M , b e. N |-> { <. I , a >. , <. J , b >. } ) $.
    cbcpcp.4 $e |- I e. C $.
    cbcpcp.5 $e |- J e. D $.
    $( The canonical bijection between a cross product and a cartesian product
       (whose set of indices is composed of two different elements).  Bourbaki
       E.II.33 .  (Contributed by FL, 26-Jun-2011.) $)
    cbcpcp $p |- ( I =/= J -> F : ( M X. N ) -1-1-onto-> X_ x e. A B ) $=
      ( wceq wi wcel vy vf ve vc wne cxp wfn crn cixp cfv wral w3a wf1o cop cpr
      vd cv cvv wa prex a1i rgen2 wb fnmpt2 mpbid wrex cab rnmpt2 elex repcpwti
      ax-mp eqcomd eqtrd wex elxp co df-opr opeq2 preq1d preq2d ovmpt2g eqtr wo
      opex preq12b vex opth2 opeq12 ex com12 com3l syl imp opth1 wn df-ne sylbi
      pm2.21 adantr eqeq2 imbi1d imbi2d mpbird eqcoms mpsyl eqeq1 fveq2 imbi12d
      jaoi 3exp eqeq2d exlimivv eqeq1d ralrimivv 3jca dff1o6 sylibr ) GHUEZFIJU
      FZUGZFUHZABCUIZRZAUQZFUJZUAUQZFUJZRZYDYFRZSZUAXSUKAXSUKZULXSYBFUMXRXTYCYK
      XRGKUQZUNZHLUQZUNZUOZURTZLJUKKIUKZXTYRXRYQKLIJYQYLITYNJTUSYMYOUTVAVBVAYRX
      TVCXRKLIJYPFOVDVAVEXRYAUBUQZYPRLJVFKIVFUBVGZYBYAYTRXRKLUBIJYPFOVHVAXRYBYT
      ABCURURUBGHIJKLMNGDTGURTPGDVIVKZHETHURTQHEVIVKVJVLVMXRYJAUAXSXSYDXSTZYFXS
      TZUSXRYJUUBUUCXRYJSZUUBYDUCUQZYSUNZRZUUEITZYSJTZUSZUSZUBVNUCVNUUCUUDSZUCU
      BYDIJVOUUKUULUCUBUUGUUJUULUUGUUJUULSUUJUUCXRUUFFUJZYGRZUUFYFRZSZSZSZSZUUS
      UUGUUCUUJUUQUUCYFUDUQZUPUQZUNZRZUUTITZUVAJTZUSZUSZUPVNUDVNUUJUUQSZUDUPYFI
      JVOUVGUVHUDUPUVCUVFUVHUVCUVFUVHSUVFUUJXRUUMUVBFUJZRZUUFUVBRZSZSZSZSZUVOUV
      CGUUEUNZHYSUNZUOZURTZUVOUVPUVQUTUUJUVSUVFUVMUUHUUIUVSUVFUVMSZSUUHUUIUVSUV
      TUUEYSFVPZUUMRUUHUUIUVSULUWAUVRRZUVTUUEYSFVQKLUUEYSIJYPUVRFUVPYOUOURYLUUE
      RYMUVPYOYLUUEGVRVSYNYSRYOUVQUVPYNYSHVRVTOWAUWBUVTSUUMUWAUUMUWARZUWBUVTUWC
      UWBUSUUMUVRRZUVTUUMUWAUVRWBUWDUVTUVFXRUVRUVIRZUVKSZSZSZUWHUWDGUUTUNZHUVAU
      NZUOZURTZUWHUWIUWJUTUVFUWLUWGUVDUVEUWLUWGSUVDUVEUWLUWGUUTUVAFVPZUVIRUVDUV
      EUWLULUWMUWKRZUWGUUTUVAFVQKLUUTUVAIJYPUWKFUWIYOUOURYLUUTRYMUWIYOYLUUTGVRV
      SYNUVARYOUWJUWIYNUVAHVRVTOWAUWNUWGSUVIUWMUVIUWMRZUWNUWGUWOUWNUSUVIUWKRZUW
      GUVIUWMUWKWBUWPUWGXRUVRUWKRZUVKSZSZUWSUWPUWQXRUVKUWQUVPUWIRZUVQUWJRZUSZUV
      PUWJRZUVQUWIRZUSZWCXRUVKSZUVPUVQUWIUWJGUUEWDHYSWDGUUTWDHUVAWDWEUXBUXFUXEU
      WTUXAUXFUWTUUEUUTRZUXAUXFSGUUEGUUTUCWFUDWFWGUXAUXGUXFUXAYSUVARZUXGUXFSHYS
      HUVAUBWFUPWFWGXRUXHUXGUVKUXHUXGUVKSSXRUXGUXHUVKUXGUXHUVKUUEYSUUTUVAWHWIWJ
      VAWKWLWJWLWMUXCUXFUXDUXCGHRZUXFGUUEHUVAUUAWNXRUXIUVKXRUXIWOUXIUVKSGHWPUXI
      UVKWRWQWJWLWSXIWQWJVAUWPUWFUWRXRUWPUWEUWQUVKUVIUWKUVRWTXAXBXCWLWIXDXEXJWM
      WJVKVAUWDUVMUWGUVFUWDUVLUWFXRUWDUVJUWEUVKUUMUVRUVIXFXAXBXBXCWLWIXDXEXJWMW
      KVKVAUVCUVHUVNUVFUVCUUQUVMUUJUVCUUPUVLXRUVCUUNUVJUUOUVKUVCYGUVIUUMYFUVBFX
      GXKYFUVBUUFWTXHXBXBXBXCWMXLWQWJVAUUGUULUURUUJUUGUUDUUQUUCUUGYJUUPXRUUGYHU
      UNYIUUOUUGYEUUMYGYDUUFFXGXMYDUUFYFXFXHXBXBXBXCWMXLWQWMWJXNXOAUAXSYBFXPXQ
      $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d I f g x y z $.  $d P f g x y z $.
    $( Definition of a projection. ` I ` is a set of indices. ` P ` is a
       cartesian product.  (Contributed by FL, 19-Jun-2011.) $)
    isprj1 $p |- ( ( P e. Q /\ I e. J ) -> ( P prj I ) =
      { <. f , g >. | ( f e. P /\ g = ( f |` I ) ) } ) $=
      ( vx vy vz wcel wa cvv cv cres wceq copab cproj elex opabbidv copab2 wrel
      co anim12i opabex2g adantr eleq2 anbi1d reseq2 eqeq2d anbi2d cxp reloprab
      df-prj reldmoprab resid2 mp2an resoprab 3eqtr2i oprabval2g 3expia sylc
      cdm ) ABJZEFJZKALJZELJZKCMZAJZDMZVGENZOZKZCDPZLJZAEQUBVMOZVCVEVDVFABREFRU
      CVCVNVDCDAVJBUDUEVEVFVNVOGHIAELLVGGMZJZVIVGHMZNZOZKZCDPZVMQVHVTKZCDPLVPAO
      ZWAWCCDWDVQVHVTVPAVGUFUGSVREOZWCVLCDWEVTVKVHWEVSVJVIVREVGUHUIUJSQIMWBOZGH
      ITZWGLLUKNZVPLJVRLJKWFKGHITGHICDUMWGUAWGVBUAWHWGOWFGHIULWFGHIUNWGUOUPWFGH
      ILLUQURUSUTVA $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A x $.  $d I f g $.  $d P f g $.
    isprj2.1 $e |- P = X_ x e. A B $.
    $( Definition of a projection. ` I ` is a set of indices. ` P ` is a
       cartesian product.  (Contributed by FL, 19-Jun-2011.) $)
    isprj2 $p |- ( ( I e. J /\ A e. C /\ A. x e. A B e. D )
      -> ( P prj I ) = { <. f , g >. | ( f e. P /\ g = ( f |` I ) ) } ) $=
      ( wcel wral w3a cvv cproj co cv wceq wa cres cixp ixpexg syl5eqel 3adant1
      copab simp1 isprj1 syl2anc ) IJLZBDLZCELABMZNFOLZUJFIPQGRZFLHRUNIUASTGHUF
      SUKULUMUJUKULTFABCUBOKABCDEUCUDUEUJUKULUGFOGHIJUHUI $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f x y $.  $d B f y $.  $d C f $.  $d D f $.  $d I f x y $.
    $( A projection is a mapping from a cartesian product to one of its
       restriction.  (Contributed by FL, 19-Jun-2011.) $)
    prjmapcp $p |- ( ( I C_ A /\ A e. C /\ A. x e. A B e. D )
      -> ( X_ x e. A B prj I ) : X_ x e. A B --> X_ x e. I B ) $=
      ( vf vy wcel wral w3a cv cixp wa wfn cfv wi cvv 3ad2ant1 imp wss cproj co
      cres wf elixp2 fnssres 3ad2ant2 sylbi com12 vex elixp ssralv pm2.27 fvres
      ex eqcomd eleq1d biimpd ralimia syl6 adantl syl5com syl5bi ax-mp sylanbrc
      resexg ralrimiva wceq copab wb ssexg 3adant3 eqid isprj2 syld3an1 fopab2g
      syl mpbid ) FBUAZBDIZCEIABJZKZGLZFUDZAFCMZIZGABCMZJZWHWFWHFUBUCZUEZWCWGGW
      HWCWDWHIZNWEFOZALZWEPZCIZAFJZWGWCWLWMVTWAWLWMQWBWLVTWMWLWDRIZWDBOZWNWDPZC
      IZABJZKVTWMQZABCWDUFWSWRXCXBWSVTWMBFWDUGUPUHUIUJSTWCWLWQWLWSXBNZWCWQABCWD
      GUKZULVTWAXDWQQWBVTXBXAAFJZQZXDWQXAAFBUMXBXGWQQWSXBXGXFWQXBXFUNXAWPAFWNFI
      ZXAWPXHWTWOCXHWOWTWNFWDUOUQURUSUTVAVBVCSVDTAFCWEWRWERIXEWDFRVGVEULVFVHWCW
      JWLHLWEVINGHVJVIZWIWKVKFRIZWAVTWBXIVTWAXJWBFBDVLVMABCDEWHGHFRWHVNVOVPGHWH
      WFWEWJVQVRVS $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f g x $.  $d A f x y $.  $d B f g $.  $d B f y $.  $d C f g x $.
    $d C f x y $.  $d D f g x $.  $d D f x y $.  $d E f g $.  $d E f y $.
    $d F f g $.
    cbicplem.1 $e |- F = ( f e. X_ x e. { A } B |-> ( f ` A ) ) $.
    cbicplem.2 $e |- ( x = A -> B = C ) $.
    $( Lemma for ~ cbicp . $)
    cbicplem $p |- ( ( A e. D /\ C e. E )
      -> F : X_ x e. { A } B -1-1-onto-> C ) $=
      ( vg vy wcel wa wceq cfv wi wral cvv a1i csn cixp wfn crn cv wf1o fvex wb
      w3a rgen fopab2ga mpbii wrex cab wex df-rex elixp2 3simpc vex simpl simpr
      3jca impbii bitri anbi1i exbii syl5bb abbidv rnmpt cmpt eqid mptfng snidg
      wal mpbi adantr eqidd fvmptg eleq1a adantl mpd sylan eqcomd jca31 ex snex
      mptexg ax-mp fneq1 fveq1 eleq1d anbi12d eqeq2d cla4ev imp exlimiv impbid1
      syl6 ralsng ax-17 fveq2 eleq12d sbciegf bitr2d anbi2d anbi1d exbidv bitrd
      alrimiv abeq2 sylibr 3eqtr4d fvmpt2 cmpbvb im2anan9 eqeq12d biimprd sylbi
      wsbc elsn com12 ralrimiv 3ad2ant3 jca elixp2a anim12i 3ad2ant1 eqfnfv syl
      mpbird eqeq2 imbi1d imbi2d syl5ibr eqeq1 syl6com mp2an pm2.43i ralrimivv
      3exp dff1o6 ) BEMZDGMZNZHABUAZCUBZUCZHUDZDOZFUEZHPZKUEZHPZOZUUJUULOZQZKUU
      FRFUUFRZUIUUFDHUFUUDUUGUUIUUQUUDBUUJPZSMZFUUFRZUUGUUSFUUFUUSUUJUUFMZBUUJU
      GZTUJUUTUUGUHUUDFUUFUURHIUKTULUUDLUEZUUROZFUUFUMZLUNZUUJUUEUCZAUEZUUJPZCM
      ZAUUERZNZUVDNZFUOZLUNZUUHDUUDUVEUVNLUVEUVAUVDNZFUOZUUDUVNUVDFUUFUPUVQUVNU
      HUUDUVPUVMFUVAUVLUVDUVAUUJSMZUVGUVKUIZUVLAUUECUUJUQUVSUVLUVRUVGUVKURUVLUV
      RUVGUVKUVRUVLFUSTUVGUVKUTUVGUVKVAVBVCVDVEVFTVGVHUUHUVFOUUDFLUUFUURHIVITUU
      DUVCDMZUVNUHZLVNDUVOOUUDUWALUUDUVTUVGUURDMZNZUVDNZFUOZUVNUUDUVTUWEUUDUVTA
      UUEUVCVJZUUEUCZBUWFPZDMZNZUVCUWHOZNZUWEUUDUVTUWLUUDUVTNZUWGUWIUWKUWGUWMUV
      CSMZAUUERUWGUWNAUUEUWNUVHUUEMZLUSTUJAUUEUVCUWFUWFVKZVLVOTUUDBUUEMZUVTUWIU
      UBUWQUUCBEVMVPZUWQUVTNZUWHUVCOZUWIABUVCUVCUUEDUWFUVHBOZUVCVQUWPVRZUVTUWTU
      WIQUWQUVCDUWHVSVTWAWBUUDUWQUVTUWKUWRUWSUWHUVCUXBWCWBWDWEUWDUWLFUWFUUESMUW
      FSMBWFAUUEUVCSWGWHUUJUWFOZUWCUWJUVDUWKUXCUVGUWGUWBUWIUUEUUJUWFWIUXCUURUWH
      DBUUJUWFWJZWKWLUXCUURUWHUVCUXDWMWLWNWRUWDUVTFUWCUVDUVTUWBUVDUVTQUVGUURDUV
      CVSVTWOWPWQUUDUWDUVMFUUDUWCUVLUVDUUDUWBUVKUVGUUBUWBUVKUHUUCUUBUVKUVJABXSU
      WBUVJABEWSUVJUWBABEUWBUWBAVNQUUBUWBAWTTUXAUVIUURCDUVHBUUJXAZJXBXCXDVPXEXF
      XGXHXIUVNLDXJXKXLUUDUUPFKUUFUUFUVAUULUUFMZNZUUDUUPUXGUUDUUPQZUUSBUULPZSMZ
      UXGUXGUXHQZQUVBBUULUGUXGUUSUXJNUUKUUROZUUMUXIOZNUXKUVAUUSUXLUXFUXJUXMUVAU
      USUXLFUUFUURSHIXMWEUXFUXJUXMKUUFUXISHFKUUFUURUXIHIBUUJUULWJXNXMWEXOUXLUXM
      UXKUXMUXKUXLUXGUUDUURUUMOZUUOQZQZQUXGUXPUXMUUDUURUXIOZUUOQZQUXGUUDUXQUUOU
      XGUUDUXQUIZUUOUUEUUEOZUVIUVHUULPZOZAUUERZNZUXSUXTUYCUXSUUEVQUXQUXGUYCUUDU
      XQUYBAUUEUWOUXQUYBUWOUXAUXQUYBQABXTUXAUYBUXQUXAUVIUURUYAUXIUXEUVHBUULXAXP
      XQXRYAYBYCYDUXSUVGUULUUEUCZNZUUOUYDUHUXGUUDUYFUXQUVAUVGUXFUYEAUUECUUJYEAU
      UECUULYEYFYGAUUEUUEUUJUULYHYIYJYTUXMUXOUXRUUDUXMUXNUXQUUOUUMUXIUURYKYLYMY
      NUXLUXHUXPUXGUXLUUPUXOUUDUXLUUNUXNUUOUUKUURUUMYOYLYMYMYNWOYPYQYRYAYSVBFKU
      UFDHUUAXK $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A f g x $.  $d A f x y $.  $d B f g $.  $d B f y $.  $d C f x $.
    $d D f x $.  $d E f $.  $d F f y $.
    cbicp.1 $e |- F = ( g e. X_ x e. { A } B |-> ( g ` A ) ) $.
    cbicp.2 $e |- ( x = A -> B = C ) $.
    $( Canonical bijection between a cartesian product indexed by a singleton
       and the base class of the elements of the 1-uple.  Bourbaki E II.32
       (Contributed by FL, 6-Jun-2011.) $)
    cbicp $p |- ( ( A e. D /\ C e. E )
      -> F : X_ x e. { A } B -1-1-onto-> C ) $=
      ( vf csn cixp cv cfv cmpt eqid fveq1 cmpbvb eqtri cbicplem ) ABCDEKGHHFAB
      LCMZBFNZOZPZKUBBKNZOZPIFKUBUDUGUEUEQBUCUFRSTJUA $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.
    $( Projection of nuple.  (Contributed by FL, 7-Nov-2011.) $)
    prjnpl $p |- ( ( B C_ A /\ F e. X_ x e. A C )
       -> ( F |` B ) e. X_ x e. B C ) $=
      ( cixp wcel wss cres cvv wfn cv cfv wral w3a wi elixp2 wa com3r syl fvres
      fnssres ssralv resexg eqcomd eleq1d ralimia biimpri 3exp syl5com com3l ex
      biimpd syl6 com13 pm2.43i com14 3imp sylbi impcom ) EABDFGZCBHZECIZACDFGZ
      VAEJGZEBKZALZEMZDGZABNZOVBVDPZABDEQVEVFVJVKVBVFVJVEVDVBVFVJVEVDPZPZPVFVBV
      BVMVFVBVBVMPZVFVBRVCCKZVNBCEUBVBVJVOVLVBVJVIACNZVOVLPVIACBUCVEVPVOVDVEVCJ
      GZVPVOVDPZECJUDVPVGVCMZDGZACNZVQVRPVIVTACVGCGZVIVTWBVHVSDWBVSVHVGCEUAUEUF
      UMUGVQVOWAVDVQVOWAVDVDVQVOWAOACDVCQUHUISTUJUKUNSTULUOUPUQURUSUT $.
      $( [7-Nov-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.  $d G x $.
    $( Union of two projections of a cartesian product.  (Contributed by FL,
       7-Nov-2011.) $)
    unprj $p |-
    ( ( F e. X_ x e. B C /\ G e. X_ x e. ( A \ B ) C /\ B C_ A )
        -> ( F u. G ) e. X_ x e. A C ) $=
      ( wcel w3a cun wfn wral wi wa c0 wceq sylancl syl syl5com wn eqcoms unexg
      cixp cdif wss cvv cv cfv 3adant3 elixp2a 3simpa ancomd disjdif fnun undif
      cin biimpi eqcomd 3ad2ant3 fneq2d mpbird 3exp 3imp cdm fndm elndif notbid
      wb eleq2 ndmfv syl6bi syl2im ralrimiv elixp2b uneq2 un0 eqtr eleq1 biimpd
      com12 ral2imi impcom eldifn uneq1 uncom imp ralunb sylanbrc raleq syl5ibr
      ex imbi2d sylbi com3l wfun df-fn ax-17 simpl anim12i ineq12 ad2ant2l fvun
      syl6eq syl2anc eleq1d ralbid 3jca elixp2 sylibr ) EACDUBZGZFABCUCZDUBZGZC
      BUDZHZEFIZUEGZXPBJZAUFZXPUGZDGZABKZHXPABDUBGXOXQXRYBXJXMXQXNEFXIXLUAUHXJX
      MXNXRXJECJZXMXNXRLZACDEUIZXMFXKJZYCYDLAXKDFUIZYFYCXNXRYFYCXNHZXRXPCXKIZJZ
      YHYCYFMCXKUOZNOYJYHYFYCYFYCXNUJUKCBULZCXKEFUMPYHBYIXPXNYFBYIOZYCXNYIBXNYI
      BOZCBUNZUPUQURUSUTVAQRVBXOYBXSEUGZXSFUGZIZDGZABKZXJXMXNYTXNXJXMYTXNYNXJXM
      YTLZLZYOUUBBYIXJUUAYMXMYSAYIKZLXJXMUUCXJXMMYSACKZYSAXKKZUUCXMXJUUDXMYFXJU
      UDLYGYFYQNOZACKZXJUUDYFUUFACYFFVCZXKOZXSCGZXSXKGZSZUUFXKFVDXSCBVEUUIUULXS
      UUHGZSZUUFUULUUNVGXKUUHXKUUHOUUKUUMXKUUHXSVHVFTXSFVIVJVKVLXJYPDGZACKUUGUU
      DLACDEVMUUOUUFYSACUUFUUOYSUUFYRYPNIZOZUUPYPOZUUOYSLZYQNYPVNYPVOUUQUURMYRY
      POUUSYRUUPYPVPUUSYPYRYPYROUUOYSYPYRDVQVRTQPVSVTQRQWAXJXMUUEXJYCXMUUELYEYC
      YPNOZAXKKZXMUUEYCUUTAXKYCEVCZCOZUUKUUJSZUUTCEVDXSBCWBUVDUUTLCUVBCUVBOZUVD
      XSUVBGZSUUTUVEUUJUVFCUVBXSVHVFXSEVIVJTVKVLXMYQDGZAXKKUVAUUELAXKDFVMUVGUUT
      YSAXKUUTUVGYSUUTYRNYQIZOZUVHYQNIZOZUVGYSLZYPNYQWCNYQWDUVIUVKMYRUVJOZUVJYQ
      OZUVLYRUVHUVJVPYQVOUVMUVNMYRYQOUVLYRUVJYQVPUVLYQYRYQYROUVGYSYQYRDVQVRTQPP
      VSVTQRQWEYSACXKWFWGWJYMYTUUCXMYSABYIWHWKWITWLWMVBXJXMXNYBYTVGZXJYCXMXNUVO
      LZYEXMYFYCUVPLZYGYFFWNZUUIMZUVQFXKWOYCUVSUVPYCEWNZUVCMZUVSUVPLECWOUWAUVSX
      NUVOUWAUVSXNHZYAYSABUWBAWPUWBXTYRDUWBUVTUVRMZUVBUUHUOZNOZXTYROUWAUVSUWCXN
      UWAUVTUVSUVRUVTUVCWQUVRUUIWQWRUHUWAUVSUWEXNUVCUUIUWEUVTUVRUVCUUIMUWDYKNUV
      BCUUHXKWSYLXBWTUHXSEFXAXCXDXEVAWLVSWLQRVBUTXFABDXPXGXH $.
      $( [7-Nov-2011] $)
  $}

  ${
    $d A f h x $.  $d B f h x $.  $d B g x $.  $d C f h $.  $d C g $.
    $d G f h $.  $d G g $.  $d G x $.
    prl.1 $e |- A e. D $.
    $( Existence of a "prolongement" of a cartesian product.  Bourbaki E.II.34
       prop. 6.  (Contributed by FL, 7-Nov-2011.) $)
    prl $p |- ( ( A. x e. A C =/= (/) /\ B C_ A /\ G e. X_ x e. B C )
     -> E. f e. X_ x e. A C G C_ f ) $=
      ( vh cv cixp wcel wex c0 wne wss w3a wi a1d exlimiv cdif wral elexi difss
      wrex ac9s n0 wa cres prjnpl eleq1 cla4egv sylc mpan sylbi imp 3adant3 cun
      unprj ssun1 sseq2 rcla4ev mpan2 syl 3exp com34 com4t 3imp com12 mpcom ) I
      JZABCUAZDKZLZIMZDNOABUBZCBPZGACDKLZQZGFJZPZFABDKZUEZVPVQVOVRVPVQVOVPWBNOZ
      VQVORZABDBEHUCUFWDVTWBLZFMWEFWBUGWFWEFWFVOVQVLBPZWFVOBCUDWGWFUHVTVLUIZVML
      ZWIVOABVLDVTUJZWJVNWIIWHVMVKWHVMUKULUMUNSTUOUOUPUQVNVSWCRIVSVNWCVPVQVRVNW
      CRVRVNVPVQWCVRVNVQVPWCVRVNVQVPWCRZVRVNVQQGVKURZWBLZWKABCDGVKUSWMWCVPWMGWL
      PZWCGVKUTWAWNFWLWBVTWLGVAVBVCSVDVEVFVGVHVITVJ $.
      $( [7-Nov-2011] $)
  $}

  ${
    $d A g $.  $d B f g $.  $d C g $.  $d f g x $.  $d A f $.  $d A x $.
    $d B x $.  $d C f $.
    prl1.1 $e |- A e. D $.
    $( Existence of a "prolongement" of a cartesian product.  Bourbaki E.II.34
       prop. 6.  (Contributed by FL, 20-Nov-2011.) $)
    prl1 $p |- ( ( A. x e. A C =/= (/) /\ B C_ A )
     -> A. g e. X_ x e. B C E. f e. X_ x e. A C g C_ f ) $=
      ( c0 wne wral wss wa cv cixp wrex wcel prl 3expia ralrimiv ) DIJABKZCBLZM
      GNZFNLFABDOPZGACDOZUAUBUCUEQUDABCDEFUCHRST $.
      $( [20-Nov-2011] $)
  $}

  ${
    $d A f g $.  $d B f g u $.  $d C f g $.  $d f g x $.  $d A x $.  $d B x $.
    prl2.1 $e |- A e. D $.
    $( Existence of a "prolongement" of a cartesian product.  Bourbaki E.II.34
       prop. 6.  (Contributed by FL, 20-Nov-2011.) $)
    prl2 $p |- ( ( A. x e. A C =/= (/) /\ B C_ A )
     -> A. g e. X_ x e. B C E. f e. X_ x e. A C g = ( f |` B ) ) $=
      ( vu wral wss wa cv wceq wrex wcel cfv wfn wi com12 c0 wne cres cixp prl1
      elixp2a cdm fndm wfun fnfun fvres eleq2 funssfv eqcomd 3exp syl6bi eqcoms
      w3a com13 com24 3imp eqeq2 imbi2d syl5ibr mpcom ancld ralrimiv ssid jctil
      3syl reximdvai syl ralimia wb ad2antlr fnssresb biimprcd syl5 imp eqfnfv3
      syl2anc rexbidva ralbidva mpbird ) DUAUBABJZCBKZLZGMZFMZCUCZNZFABDUDZOZGA
      CDUDZJCCKZIMZCPZWPWHQZWPWJQZNZLZICJZLZFWLOZGWNJZWGWHWIKZFWLOZGWNJXEABCDEF
      GHUEXGXDGWNWHWNPZWHCRZXGXDSZACDWHUFZXIWHUGZCNZXJCWHUHXMXFXCFWLWIWLPZXMXFX
      CSZXNWIBRZWIUIZXMXOSABDWIUFZBWIUJXQXMXFXCXQXMXFURZXBWOXSXAICXSWQWTWQXSWTW
      SWPWIQZNZWQXSWTSZWPCWIUKWQYBYAXSWRXTNZSXSWQYCXQXMXFWQYCSZXMXQXFYDSXMWQXFX
      QYCWQXFXQYCSSZSCXLCXLNWQWPXLPZYECXLWPULXQXFYFYCXQXFYFYCXQXFYFURXTWRWPWIWH
      UMUNUOUSUPUQUTTVATYAWTYCXSWSXTWRVBVCVDVETVFVGCVHVIUOVJTVKVLVLVMVLWGWMXDGW
      NWGXHLZWKXCFWLYGXNLXIWJCRZWKXCVNXHXIWGXNXKVOYGXNYHXNXPYGYHXRWFXPYHSWEXHXP
      YHWFBCWIVPVQVOVRVSICCWHWJVTWAWBWCWD $.
      $( [20-Nov-2011] $)
  $}


  ${
    $d A a f x y $.  $d A f u v x $.  $d B a f y $.  $d B f u v $.  $d C f y $.
    $d D a f y $.  $d I a f x y $.  $d I f u v x $.
    $( A projection is a mapping from a cartesian product onto one of its
       restriction.  Bourbaki E.II.33 prop. 5.  (Contributed by FL,
       20-Nov-2011.) $)
    prjmapcp2 $p |- ( ( I C_ A /\ A e. C /\
      ( A. x e. A B e. D /\ A. x e. A B =/= (/) ) )
      -> ( X_ x e. A B prj I ) : X_ x e. A B -onto-> X_ x e. I B ) $=
      ( vy vf va vu vv wcel wral wa cixp cv wceq wrex wi cvv wss c0 cproj co wf
      wne w3a cfv wfo prjmapcp 3adant3r cres sseq2 anbi12d ixpeq1 eleq2d anbi1d
      raleq rexbidv2 ralbidv imbi12d pm3.2 adantl vex prl2 syl6com vtoclg com12
      3imp copab simpr resexg ax-mp jctir reseq1 eqid fvopab4g simp2 simp3l jca
      adantr ixpexg syl ssexg 3adant3 isprj1 syl2anc eqcomd fveq1d biimpcd 3syl
      eqeq1d pm2.43i eqeq2d rexbidva mpbird dffo3 sylanbrc ) FBUAZBDLZCELZABMZC
      UBUFZABMZNZUGZABCOZAFCOZXGFUCUDZUEZGPZHPZXIUHZQZHXGRZGXHMZXGXHXIUIWSWTXBX
      JXDABCDEFUJUKXFXPXKXLFULZQZHXGRZGXHMZWSWTXEXTWTWSXEXTSZFIPZUAZXAAYBMZXCAY
      BMZNZXRHAYBCOZRZGXHMZSZSWSYASIBDYBBQZYCWSYJYAYBBFUMYKYFXEYIXTYKYDXBYEXDXA
      AYBBURXCAYBBURUNYKYHXSGXHYKXRXRHYGXGYKXLYGLXLXGLZXRYKYGXGXLAYBBCUOUPUQUSU
      TVAVAYFYCYEYCNZYIYEYCYMSYDYEYCVBVCAYBFCTHGIVDVEVFVGVHVIXFXOXSGXHXFXNXRHXG
      XFYLNZXMXQXKYNXMXQQZYNYLXQTLZNXLJPZXGLKPYQFULZQNJKVJZUHZXQQZYNYOSYNYLYPXF
      YLVKXLTLYPHVDXLFTVLVMVNJKXLYRXQXGTYSYQXLFVOYSVPVQYNUUAYOYNYTXMXQYNXLYSXIY
      NXIYSYNXGTLZFTLZXIYSQYNWTXBNZUUBXFUUDYLXFWTXBWSWTXEVRWSWTXBXDVSVTWAABCDEW
      BWCXFUUCYLWSWTUUCXEFBDWDWEWAXGTJKFTWFWGWHWIWLWJWKWMWNWOUTWPHGXGXHXIWQWR
      $.
      $( [20-Nov-2011] $)
  $}

  ${
    $d A f x z $.  $d B f y z $.  $d C f z $.  $d D y z $.  $d X z $.
    $d x y z $.
    dstr.1 $e |- ( y = ( f ` x ) -> C = D ) $.
    dstr.2 $e |- X = X_ x e. A B $.
    dstr.3 $e |- A e. E $.
    $( Distribution of union over intersection.  Bourbaki E.II.35 prop. 8.
       (Contributed by FL, 18-Jun-2011.) $)
    dstr $p |- U_ x e. A |^|_ y e. B C = |^|_ f e. X U_ x e. A D $=
      ( vz cv wcel wrex wral syl 3imtr4i wn ciin ciun eliun cixp eleq2i elixp2b
      wi cfv wceq eleq2d rcla4v ralimi rexim sylbi com12 ralrimiv ralbii sylibr
      cvv wb vex eliin ax-mp rexbii ralnex crab c0 wne rabn0 biimpri elexi ac9s
      wex wss ss2ixp ssrab2 a1i mprg sseli syl6eleqr notbid elrab simprbi eximi
      wa jca n0 df-rex sylib rexnal 3imtr3i sylbir con4i impbii eqriv ) MACBDEU
      AZUBZGIACFUBZUAZMNZWQOZWTWSOZXAWTWPOZACPZXBAWTCWPUCZWTEOZBDQZACPZWTWROZGI
      QZXDXBXHWTFOZACPZGIQZXJXHXLGIGNZIOZXHXLXOXNACDUDZOZXHXLUGZIXPXNKUEXQANZXN
      UHZDOZACQZXRACDXNUFYBXGXKUGZACQXRYAYCACXFXKBXTDBNXTUIZEFWTJUJZUKULXGXKACU
      MRRUNUOUPXIXLGIAWTCFUCUQZURXCXGACWTUSOZXCXGUTMVAZBWTDEUSVBVCVDZYGXBXJUTYH
      GWTIWRUSVBVCZSUNXJXDXBXAXMXHXJXDXHXMXHTXGTZACQZXMTZXGACVEXFTZBDPZACQZXLTZ
      GIPZYLYMYPXKTZACQZGIPZYRYPYNBDVFZVGVHZACQZUUAYOUUCACUUCYOYNBDVIVJULUUDACU
      UBUDZVGVHZUUAACUUBCHLVKVLXNUUEOZGVMXOYTWEZGVMUUFUUAUUGUUHGUUGXOYTUUGXNXPI
      UUEXPXNUUBDVNZUUEXPVNACACUUBDVOUUIXSCOYNBDVPVQVRVSKVTUUGXTUUBOZACQYTACUUB
      XNUFUUJYSACUUJYAYSYNYSBXTDYDXFXKYEWAWBWCULRWFWDGUUEWGYTGIWHSUNRYTYQGIXKAC
      VEVDWIYOYKACXFBDWJUQXLGIWJWKWLWMYFYISYJXESWNWO $.
      $( [18-Jun-2011] $)

  $}


