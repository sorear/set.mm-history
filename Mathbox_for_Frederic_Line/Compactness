$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Compactness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The singleton of the empty set is compact.  (Contributed by FL,
     2-Aug-2009.) $)
  sinempcomp $p |- { (/) } e. Comp $=
    ( c0 csn ctop cfn cin wcel ccomp elin sn0top snfi mpbir2an fintopcomp ax-mp
    ) ABZCDEFZNGFONCFNDFNCDHIAJKNLM $.
    $( [2-Aug-2009] $)

  $( The indiscrete topology is compact.  (Contributed by FL, 2-Aug-2009.) $)
  indcomp $p |- { (/) , A } e. Comp $=
    ( cpr ctop cfn cin wcel ccomp elin indistop prfi mpbir2an fintopcomp ax-mp
    c0 ) NABZCDEFZOGFPOCFODFOCDHAINAJKOLM $.
    $( [2-Aug-2009] $)

  $( A topology whose underlying set is finite is compact.  (Contributed by FL,
     22-Dec-2008.) $)
  topunfincomp $p |- ( ( J e. Top /\ U. J e. Fin ) -> J e. Comp ) $=
    ( cuni cfn wcel ctop ccomp unfinsef wa cin elin fintopcomp sylbir sylan2 )
    ABCDAEDZACDZAFDZAGNOHAECIDPAECJAKLM $.
    $( [22-Dec-2008] $)

  ${
    $d x y J $.
    $( A compact topology is a topology.  (Contributed by FL, 2-Aug-2009.)
       (Revised by Mario Carneiro, 15-Dec-2013.) $)
    cmptop $p |- ( J e. Comp -> J e. Top ) $=
      ( vx vy ccomp wcel ctop cuni cv wceq cpw cfn cin wrex wral iscomp simplbi
      wi ) ADEAFEAGZBHZGIRCHGICSJKLMQBAJNBCAOP $.
      $( [15-Dec-2013] $) $( [2-Aug-2009] $)
  $}

  ${
    $( The subspace topology induced by a finite part of the underlying set of
       a topology is compact.  (Contributed by FL, 2-Aug-2009.)  (Revised by
       Mario Carneiro, 15-Dec-2013.) $)
    stfincomp $p |- ( ( J e. Top /\ A e. Fin ) ->
      ( subSp ` <. A , J >. ) e. Comp ) $=
      ( ctop wcel cfn wa cop csubsp cfv cuni cin ccomp eqid subspint inss1 ssfi
      wss mpan2 stoig3 sylan2 stoig2 biimpa syl2an topunfincomp syl2anc eqeltrd
      wceq inss2 eleq1 ) BCDZAEDZFZABGHIABJZKZBGHIZLABEUMUMMNULUOCDZUOJZEDZUOLD
      UKUJUNEDZUPUKUNAQUSAUMOAUNPRZUNBESTUJUNUQUGZUSURUKUJUNUMQVAAUMUHUNBUARUTV
      AUSURUNUQEUIUBUCUOUDUEUF $.
      $( [15-Dec-2013] $) $( [2-Aug-2009] $)
  $}

  ${
    $d A b f x z $.  $d A f o x $.  $d J a f z $.  $d J b f x z $.
    $d J f o x $.  $d X b f x z $.  $d X f o x $.
    bwt2.1 $e |- X = U. J $.
    $( The glorious Bolzano-Weierstrass theorem.  Certainly the first general
       topology theorem ever proved.  In his course Weierstrass called it a
       lemma.  He certainly didn't know how famous this theorem would be.  He
       used an euclidian space instead of a general compact space.  And he was
       not conscious of the Heine-Borel property.  Cantor was one of his
       students.  He used the concept of neighborhood and limit point invented
       by his master when he studied the linear point sets and the rest of the
       general topology followed from that.  (Contributed by FL,
       2-Aug-2009.) $)
    bwt2 $p |- ( ( J e. Comp /\ A C_ X /\ -. A e. Fin ) ->
     E. x e. X x e. ( ( limPt ` J ) ` A ) ) $=
      ( vo vz wcel cfn wn cv wrex cdif cin c0 wi wa syl wceq com13 vf va vb wss
      ccomp w3a clp cfv csn wne wral wb ctop cmptop islp3 imp31 rexbidva notbid
      3exp 3adant3 negcmpprcal1 wf wex cvv cuni uniexg syl5eqel eleq2 nne ineq1
      eqeq1d syl5bb anbi12d ac6sg 3ad2ant1 crn cpw frn rnex sylibr adantr uniss
      vex elpw wal df-ral ax-17 eqcomi eleq2i pm2.27 cdm fdm biimpd wfun fvelrn
      ffun ex elunii idd a1i syl6 com4l com23 mpcom com12 syl6com eqcoms impcom
      pm2.43a sylbi alimd syl5bi 3impia dfss2 simp1 eqssd imp jca iscomp eqeq2d
      unieq pweq ineq1d rexeqdv imbi12d rcla4 elin eqeq1i sseq2 com3l hbim eqtr
      difindir eqtr2 mpan adantl com3r syl6bi rexlimiv syl8 elelpwi wfn fvelrnb
      isunscov hbra1 hbre1 ineq2 eleq1d difeq1 0dif difabs ineq2i incom difeq1i
      ffn 0fin eleq1i eleq1 syl5bbr mpbiri difinf mpan2 con4i pm2.24d ax-mp mpi
      snfi imim2i a4s rexlimi com24 com4r 3impd 3expd 3imp exlimiv syld pm2.18d
      sylbid ) CUEHZBDUDZBIHJZUFZAKZBCUGUHUHHZADLZUWCUWFJZUWDFKZHZUWHBUWDUIZMZN
      ZOUJZPFCUKZADLZJZUWFUVTUWAUWGUWPULUWBUVTUWAQZUWFUWOUWQUWEUWNADUVTUWAUWDDH
      ZUWEUWNULZUVTCUMHZUWAUWRUWSPPCUNUWTUWAUWRUWSFUWDBCDEUOUSRUPUQURUTUWPUWIUW
      MJZQZFCLADUKZUWCUWFUWIUWMAFDCVAUWCUXCDCUAKZVBZUWDUWDUXDUHZHZUXFUWKNZOSZQZ
      ADUKZQZUAVCZUWFUVTUWAUXCUXMPZUWBUVTDVDHUXNUVTDCVEZVDECUEVFVGUXBUXJAFDCUAV
      DUWHUXFSZUWIUXGUXAUXIUWHUXFUWDVHUXAUWLOSUXPUXIUWLOVIUXPUWLUXHOUWHUXFUWKVJ
      VKVLVMVNRVOUXMUWCUWFUXLUWCUWFPZUAUXDVPZCVQZHZUXOUXRVEZSZQZUXLUXQUXLUXTUYB
      UXEUXTUXKUXEUXRCUDZUXTDCUXDVRZUXRCUXDUAWCVSWDVTWAUXEUXKUYBUYAUXOUDZUXEUXK
      UYBPUXEUYDUYFUYEUXRCWBRUYFUXEUXKUYBUYFUXEUXKUFZUXOUYAUYGUWDUXOHZUWDUYAHZP
      ZAWEZUXOUYAUDUYFUXEUXKUYKUXKUWRUXJPZAWEZUYFUXEQZUYKUXJADWFZUYNUYLUYJAUYNA
      WGUYHUYLUYNUYIUYHUWRUYLUYNUYIPZPUXODUWDDUXOEWHZWIUYLUWRUYPUWRUYLUXJUWRUYP
      PZUWRUXJWJUXGUYRUXIUYNUWRUXGUYIUXEUYFUWRUXGUYIPZPZUXDWKZDSUXEUYFUYTPZDCUX
      DWLUXEVUBPDVUAUWRDVUASZUXEUYFUYSVUCUWRUWDVUAHZUXEUYFUYSPZPZVUCUWRVUDDVUAU
      WDVHWMUXEVUDVUEUXDWNZUXEVUDVUEPDCUXDWPVUGVUDUXEVUEVUGVUDUXFUXRHZVUFVUGVUD
      VUHUWDUXDWOWQUXGVUHUXEUYFUYIUXGVUHUYIUXEUYFUYIPPUXGVUHUYIUWDUXFUXRWRWQUYF
      UXEUYIUYIUXEUYIUYIPPUYFUXEUYIWSWTTXAXBXAXCXDXEXFXBXGXDXHTWAXAXIXJTXKXLXMA
      UXOUYAXNVTUYFUXEUXKXOXPUSXDXQXRUWCUXLUYCUWFUVTUWAUWBUXLUYCUWFPZPZUVTUWTUX
      OUBKZVEZSZUXOGKZVEZSZGVUKVQZINZLZPZUBUXSUKZQUWAUWBVUJPPZUBGCXSVVAVVBUWTVV
      AUWAUWBUXLVUIUYCVVAUWAUWBUXLUFZUWFUXTUYBVVAVVCUWFPZPUXTVVAUYBVVDUXTVVAUYB
      VUPGUXRVQZINZLZVVDVUTUYBVVGPZUBUXRUXSVVHUBWGVUKUXRSZVUMUYBVUSVVGVVIVULUYA
      UXOVUKUXRYAXTVVIVUPGVURVVFVVIVUQVVEIVUKUXRYBYCYDYEYFVVGUWAUWBUXLUWFVUPUWA
      UWBUXLUWFPZPZPZGVVFVUNVVFHVUNVVEHZVUNIHZQZVUPVVLPVUNVVEIYGVUPUWAVVOVVKVUP
      DVUOSZUWAVVOVVKPZPUXODVUOUYQYHVVPUWABVUOUDZVVQDVUOBYIVVOVVRVVKVVMVVNVVRVV
      KPVVNVVRUWBVVMVVJVVNVVRUWBBUCKZNZIHZJZUCVUNLZVVMVVJPZUWBVVNVVRVWCUWBVVNVV
      RVWCUCBVUNUUDUSYJVWBVWDUCVUNVVSVUNHZVVMVWBVVJVWEVVMVVSUXRHZVWBVVJPVWEVVMV
      WFVVSVUNUXRUUAWQUXLVWBVWFUWFUXEUXKVWBVWFUWFPPZUXEUXDDUUBZUXKVWGPDCUXDUUOV
      WHVWFVWBUXKUWFVWHVWFUXFVVSSZADLVWBUXKUWFPZPZADVVSUXDUUCVWIVWKADVWBVWJAVWB
      AWGUXKUWFAUXJADUUEUWEADUUFYKYKVWIVWBUWRVWJVWIVWBBUXFNZIHZJZUWRVWJPVWBVWNP
      VVSUXFVVSUXFSZVWBVWNVWOVWAVWMVWOVVTVWLIVVSUXFBUUGUUHURWMXGUXKUWRVWNUWFUXK
      UYMUWRVWNUWFPZPZUYOUYLVWQAUXJVWPUWRUXIVWPUXGUXIUXHUWJMZOUWJMZSZVWPUXHOUWJ
      UUIVWTVWSOSZVWPUWJUUJVWTVXAVWROSZVWPVWTVXAVXBVWRVWSOYLWQVWRUXFUWJMZUWKUWJ
      MZNZSVXBVWPPZUXFUWKUWJYMVXFVXEVWRVXEVWRSZVXBVWPVXGVXBQVXEOSZVWPVXEVWROYLV
      XHVWMUWFVXEVXCUWKNZSZVXHVWMVXDUWKVXCBUWJUUKUULVXJVXHQVXIOSZVWMVXEVXIOYNVX
      IUXFBNZUWJMZSZVXKVWMVXMVXIUXFBUWJYMWHVXNVXKQVXMOSZVWMVXIVXMOYNVXOVWLUWJMZ
      IHZVWMVXOVXQOIHZUUPVXQVXMIHVXOVXRVXMVXPIVXLVWLUWJUXFBUUMUUNUUQVXMOIUURUUS
      UUTVWMVXQVWNUWJIHVXQJUWDUVGVWLUWJUVAUVBUVCRRYORYOUVDRWQXGUVEXAUVFRYPUVHUV
      IXJTXAYQUVJYRUVKRXQTXAXCYSYTUVLXQXEYRXJYQXJYSUVMYTXCXQYJUVNYPXJUVOTXDUVPX
      EUVQXLUVSUVR $.
      $( [15-Dec-2013] $) $( [2-Aug-2009] $)
  $}

