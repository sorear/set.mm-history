$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Compactness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Comp $.

  $( Extend class notation with the class of all compact spaces. $)
  ccomp $a class Comp $.

  ${ $d x y z $.
    $( Definition of a compact topology. A topology is compact iff any open
    covering of its underlying set contains a finite sub-covering
    (Heine-Borel property).
    Bourbaki TG I.59  prop C''' . Note: Bourbaki
    uses the term quasi-compact topology but it is not the modern usage
    (which we follow). $)
    df-comp $a  |- Comp = { x e. Top | A. y e. P~ x ( U. x = U. y
      -> E. z e. ( P~ y i^i Fin )  U. x = U. z ) } $.
  $}

  ${ $d J x y z $.
    $( The predicate "is a compact topology". $)
    iscomp $p |- ( J e. Comp <-> ( J e. Top /\ A. y e. P~ J
      ( U. J = U. y -> E. z e. ( P~ y i^i Fin )  U. J = U. z ) ) ) $=
      ( vx cv cuni wceq cpw cfn cin wrex wi wral ctop ccomp pweq unieq eqeq1d
      rexbidv imbi12d raleq12d df-comp elrab2 ) DEZFZAEZFZGZUEBEFZGZBUFHIJZKZLZ
      AUDHZMCFZUGGZUOUIGZBUKKZLZACHZMDCNOUDCGZUMUSAUNUTUDCPVAUHUPULURVAUEUOUGUD
      CQZRVAUJUQBUKVAUEUOUIVBRSTUADABUBUC $.
      $( [22-Dec-2008] $) $( [22-Dec-2008] $)
  $}

  ${ $d J y z $.
  $( A finite topology is compact. $)
  fintopcomp $p |- ( J e. ( Top i^i Fin ) -> J e. Comp ) $=
    ( vy vz ctop cfn cin wcel cuni cv wceq cpw wrex wi wral wa ccomp inss1
    sseli inss2 wss ssfi visset elpw sylan2b pwid elin unieq eqeq2d rcla4ev ex
    sylbir mpan syl r19.21aiva jca iscomp sylibr ) ADEFZGZADGZAHZBIZHZJZVACIZHZ
    JZCVBKZEFZLZMZBAKZNZOAPGUSUTVMURDADEQRUSAEGZVMUREADESRVNVKBVLVNVBVLGZOVBEGZ
    VKVNVBATVPVOAVBUAVBABUBZUCUDVBVHGZVPVKVBVQUEVRVPOVBVIGZVKVBVHEUFVSVDVJVGVDC
    VBVIVEVBJVFVCVAVEVBUGUHUIUJUKULUMUNUMUOBCAUPUQ $.
    $( [22-Dec-2008] $) $( [22-Dec-2008] $) $}

  $( The singleton of the empty set is compact. $)
  sinempcomp $p |- { (/) } e. Comp $=
    ( c0 csn ctop cfn cin wcel ccomp elin sn0top snfi mpbir2an fintopcomp
    ax-mp ) ABZCDEFZNGFONCFNDFNCDHIAJKNLM $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( The subspace topology induced by the empty set is compact. $)
  subempcomp $p |- ( J e. Top -> ( subSp ` <. (/) , J >. ) e. Comp ) $=
    ( ctop wcel c0 cop csubsp cfv csn ccomp subspemp2 sinempcomp syl6eqel ) ABC
    DAEFGDHIAJKL $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( The indiscrete topology is compact. $)
  indcomp $p |- { (/) , A } e. Comp $=
    ( c0 cpr ctop cfn cin wcel ccomp elin indistop prfi mpbir2an fintopcomp
    ax-mp ) BACZDEFGZOHGPODGOEGODEIAJBAKLOMN $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  ${
    subsincomp.1 $e |- X = U. J $.
    $( The subspace topology induced by a singleton is compact. $)
    subsincomp $p |- ( ( J e. Top /\ A e. X ) ->
    ( subSp ` <. { A } , J >. ) e. Comp ) $=
      ( ctop wcel wa csn cop csubsp cfv c0 cpr ccomp subtopsin2 indcomp
      syl6eqel ) BEFACFGAHZBIJKLRMNABCDORPQ $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  $( A topology whose underlying set is finite is compact. $)
  topunfincomp $p |- ( ( J e. Top /\ U. J e. Fin ) -> J e. Comp ) $=
    ( ctop wcel cfn ccomp cuni wa cin elin fintopcomp sylbir unfinsef sylan2 )
    ABCZADCZAECZAFDCNOGABDHCPABDIAJKALM $.
    $( [22-Dec-2008] $) $( [22-Dec-2008] $)

  ${ $d x y z $.
    $( A compact topology is a topology. $)
    cmptop $p |- Comp (_ Top $=
      ( vx vy vz ccomp ctop cv wcel cuni wceq cpw cfn cin wrex wi wral iscomp
      pm3.26bi ssriv ) ADEAFZDGSEGSHZBFZHITCFHICUAJKLMNBSJOBCSPQR $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${
    stfincomp.1 $e |- X = U. J $.
    $( The subspace topology induced by a finite part of the underlying set
       of a topology is compact. $)
    stfincomp $p |- ( ( J e. Top /\ A e. ( P~ X i^i  Fin ) ) ->
      ( subSp ` <. A , J >. ) e. Comp ) $=
      ( ctop wcel cpw cfn cin wa cop csubsp cfv cuni ccomp wss wi elin elpwi
      anim1i sylbi sseq2i biimpi stoig3 expcom adantr imp wceq stoig2 simplr
      eqeltrd jca ex 3syl impcom topunfincomp syl ) BEFZACGZHIFZJABKLMZEFZVANZH
      FZJZVAOFUTURVEUTACPZAHFZJZABNZPZVGJZURVEQUTAUSFZVGJVHAUSHRVLVFVGACSTUAVFV
      JVGVFVJCVIADUBUCTVKURVEVKURJZVBVDVKURVBVJURVBQVGURVJVBABUDUEUFUGVMVCAHVKU
      RVCAUHZVJURVNQVGURVJVNABUIUEUFUGVJVGURUJUKULUMUNUOVAUPUQ $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${ $d A b f x z $. $d A f o x $. $d J a f z $. $d J b f x z $. $d J f o x $.
     $d X b f x z $. $d X f o x $.
   bwt2.1 $e |- X = U. J $.
   $( Todo: recalculate the distinct variables statement. $)
   $( The  glorious Bolzano-Weierstrass  theorem.  Certainly the  first general
      topology  theorem ever  proved. In  his  course Weierstrass  called it  a
      lemma. He  certainly didn't  know how famous  this theorem would  be.  He
      used an  euclidian space instead of  a general compact space.  And he was
      not  conscious  of the  Heine-Borel  property.   Cantor  was one  of  his
      students. He used the concept of neighborhood and limit point invented by
      his master  when he  studied the linear  point sets  and the rest  of the
      general topology followed from that. $)
   bwt2 $p |- ( ( J e. Comp /\ A (_ X /\ -. A e. Fin ) ->
     E. x e. X  x e. ( ( limPt ` J ) ` A ) ) $=
     ( vo vf va vz vb ccomp wcel wss cfn wn w3a cv clp cfv wrex wel csn cdif
     cin c0 wne wi wral wb wa ctop cmptop sseli islp3 3exp syl imp31 rexbidva
     notbid 3adant3 wf wceq wex cvv cuni uniexg syl5eqel eleq2 ineq1 eqeq1d
     nne syl5bb anbi12d ac6sg 3ad2ant1 crn cpw frn visset rnex elpw sylibr
     adantr uniss wal ax-17 eqcomi eleq2i pm2.27 cdm fdm biimpd wfun ffun
     fvelrn ex elunii idd a1i com13 syl6 com4l com23 mpcom com12 syl6com
     eqcoms impcom pm2.43a sylbi 19.20d df-ral syl5ib 3impia dfss2 3simp1
     eqssd imp jca iscomp unieq eqeq2d pweq ineq1d rexeq1d imbi12d rcla4 elin
     eqeq1i sseq2 isunscov com3l bepart wfn ffn fvelrnb hbra1 hbre1 hbim ineq2
     eleq1d difeq1 0dif eqtr difindir remcon ineq2i eqtr2 emfin eleq1 incom
     difeq1i eleq1i syl5bbr mpbiri snfi islfin mpan2 con4i mpan pm2.24d ax-mp
     mpi adantl imim2i a4s com3r r19.23ai syl6bi com24 r19.23aiv syl8 com4r
     3impd 3expd 3imp 19.23aiv syld negcmpprcal1 sylbid pm2.01bd ) CKLZBDMZBNLO
     ZPZAQZBCRSSLZADTZUWEUWHOZAFUAZFQZBUWFUBZUCZUDZUEUFZUGFCUHZADTZOZUWHUWBUWCU
     WIUWRUIUWDUWBUWCUJZUWHUWQUWSUWGUWPADUWBUWCUWFDLZUWGUWPUIZUWBCUKLZUWCUWTUXA
     UGUGKUKCULUMUXBUWCUWTUXAFUWFBCDEUNUOUPUQURUSUTUWEUWJUWOOZUJZFCTADUHZUWHUWR
     UWEUXEDCGQZVAZUWFUWFUXFSZLZUXHUWMUDZUEVBZUJZADUHZUJZGVCZUWHUWBUWCUXEUXOUGZ
     UWDUWBDVDLUXPUWBCVEZVDDCKVFEVGUXDUXLAFDCGUWKUXHVBZUWJUXIUXCUXKUWKUXHUWFVHU
     XRUWNUEVBUXKUXCUXRUWNUXJUEUWKUXHUWMVIVJUWNUEVKVLVMVNUPVOUXOUWEUWHUXNUWEUWH
     UGZGUXFVPZCVQZLZUXQUXTVEZVBZUJZUXNUXSUXNUYBUYDUXGUYBUXMUXGUXTCMZUYBDCUXFVR
     ZUXTCUXFGVSVTWAWBWCUXGUXMUYDUYCUXQMZUXGUXMUYDUGUXGUYFUYHUYGUXTCWDUPUYHUXGU
     XMUYDUYHUXGUXMPZUXQUYCUYIUWFUXQLZUWFUYCLZUGZAWEZUXQUYCMUYHUXGUXMUYMUYHUXGU
     JZUWTUXLUGZAWEZUYMUXMUYNUYOUYLAUYNAWFUYJUYOUYNUYKUYJUWTUYOUYNUYKUGZUGUXQDU
     WFDUXQEWGZWHUYOUWTUYQUWTUYOUXLUWTUYQUGZUWTUXLWIUXIUYSUXKUYNUWTUXIUYKUXGUYH
     UWTUXIUYKUGZUGZUXFWJZDVBUXGUYHVUAUGZDCUXFWKUXGVUCUGDVUBUWTDVUBVBZUXGUYHUYT
     VUDUWTUWFVUBLZUXGUYHUYTUGZUGZVUDUWTVUEDVUBUWFVHWLUXGVUEVUFUXFWMZUXGVUEVUFU
     GDCUXFWNVUHVUEUXGVUFVUHVUEUXHUXTLZVUGVUHVUEVUIUWFUXFWOWPUXIVUIUXGUYHUYKUXI
     VUIUYKUXGUYHUYKUGUGUXIVUIUYKUWFUXHUXTWQWPUYHUXGUYKUYKUXGUYKUYKUGUGUYHUXGUY
     KWRWSWTXAXBXAXCXDXEXFXBXGXDXHWTWCXAXIXJWTXKUXLADXLZXMXNAUXQUYCXOWBUYHUXGUX
     MXPXQUOXDXRXSUWEUXNUYEUWHUWBUWCUWDUXNUYEUWHUGZUGZUWBUXBUXQHQZVEZVBZUXQIQZV
     EZVBZIVUMVQZNUDZTZUGZHUYAUHZUJUWCUWDVULUGUGZHICXTVVCVVDUXBVVCUWCUWDUXNVUKU
     YEVVCUWCUWDUXNPZUWHUYBUYDVVCVVEUWHUGZUGUYBVVCUYDVVFUYBVVCUYDVURIUXTVQZNUDZ
     TZVVFVVBUYDVVIUGZHUXTUYAVVJHWFVUMUXTVBZVUOUYDVVAVVIVVKVUNUYCUXQVUMUXTYAYBV
     VKVURIVUTVVHVVKVUSVVGNVUMUXTYCYDYEYFYGVVIUWCUWDUXNUWHVURUWCUWDUXNUWHUGZUGZ
     UGZIVVHVUPVVHLVUPVVGLZVUPNLZUJZVURVVNUGVUPVVGNYHVURUWCVVQVVMVURDVUQVBZUWCV
     VQVVMUGZUGUXQDVUQUYRYIVVRUWCBVUQMZVVSDVUQBYJVVQVVTVVMVVOVVPVVTVVMUGVVPVVTU
     WDVVOVVLVVPVVTUWDBJQZUDZNLZOZJVUPTZVVOVVLUGZUWDVVPVVTVWEUWDVVPVVTVWEJBVUPY
     KUOYLVWDVWFJVUPJIUAZVVOVWDVVLVWGVVOVWAUXTLZVWDVVLUGVWGVVOVWHVWAVUPUXTYMWPU
     XNVWDVWHUWHUXGUXMVWDVWHUWHUGUGZUXGUXFDYNZUXMVWIUGDCUXFYOVWJVWHVWDUXMUWHVWJ
     VWHUXHVWAVBZADTVWDUXMUWHUGZUGZADVWAUXFYPVWKVWMADVWDVWLAVWDAWFUXMUWHAUXLADY
     QUWGADYRYSYSVWKVWDUWTVWLVWKVWDBUXHUDZNLZOZUWTVWLUGVWDVWPUGVWAUXHVWAUXHVBZV
     WDVWPVWQVWCVWOVWQVWBVWNNVWAUXHBYTUUAUSWLXGUXMUWTVWPUWHUXMUYPUWTVWPUWHUGZUG
     ZVUJUYOVWSAUXLVWRUWTUXKVWRUXIUXKUXJUWLUCZUEUWLUCZVBZVWRUXJUEUWLUUBVXBVXAUE
     VBZVWRUWLUUCVXBVXCVWTUEVBZVWRVXBVXCVXDVWTVXAUEUUDWPVWTUXHUWLUCZUWMUWLUCZUD
     ZVBVXDVWRUGZUXHUWMUWLUUEVXHVXGVWTVXGVWTVBZVXDVWRVXIVXDUJVXGUEVBZVWRVXGVWTU
     EUUDVXJVWOUWHVXGVXEUWMUDZVBZVXJVWOVXFUWMVXEBUWLUUFUUGVXLVXJUJVXKUEVBZVWOVX
     GVXKUEUUHVXKUXHBUDZUWLUCZVBZVXMVWOVXOVXKUXHBUWLUUEWGVXPVXMUJVXOUEVBZVWOVXK
     VXOUEUUHVXQVWNUWLUCZNLZVWOVXQVXSUENLZUUIVXQVXONLVXTVXSVXOUENUUJVXOVXRNVXNV
     WNUWLUXHBUUKUULUUMUUNUUOVWOVXSVWPUWLNLVXSOUWFUUPVWNUWLUUQUURUUSUPUPUUTUPUU
     TUVAUPWPXGUVBXAUVCUPUVDUVEUVFXJWTXAUVGUVHUVIUVJUPXRWTXAXCUVKUVLUVMXRXEUVIX
     JUVGXJUVKUVNUVLXCXRYLUVOUVDXJUVPWTXDUVQXEUVRUWJUWOAFDCUVSXMUVTUWA $.
     $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

