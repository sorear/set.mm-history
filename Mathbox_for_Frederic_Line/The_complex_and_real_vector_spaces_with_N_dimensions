$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
   The complex  and real  vector spaces with N dimensions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +cv $. $( Addition of complex vectors. $)

  $( Extends class notation with addition of complex vectors. $)
  cplcv $a class +cv $.

  ${
    $d n u v x $.
    $( Addition of complex vectors in a space of dimension ` n ` .  (We might
       also define those vectors using the elements and generic operations of a
       field.  There are many structures that can be set on the elements of
       ( CC ^m ( 1 ... n ) ) or ( RR ^m ( 1 ... n ) ) but it is traditional to
       view them as vectors so I will give them this name.)  Experimental. $)
    df-addcv $a |- +cv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) ) ,
        v e. ( CC ^m ( 1 ... n ) ) |->
        ( x e. ( 1 ... n ) |-> ( ( u ` x ) + ( v ` x ) ) ) ) ) $.
  $}

  ${
    isaddrv.1 $e |- +w = ( +cv ` N ) $.
    ${
      $d x v u U $.  $d x v u n N $.  $d x v V $.
      $( Addition of complex vectors.  Experimental.  (Contributed by FL,
         14-Sep-2013.) $)
      isaddrv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
      V e. ( CC ^m ( 1 ... N ) ) ) -> ( U +w V ) =
      ( x e. ( 1 ... N ) |-> ( ( U ` x ) + ( V ` x ) ) ) ) $=
        ( vu vv wcel cc c1 cfz co cmap cv cfv caddc cmpt wceq cvv vn cn w3a a1i
        cmpt2 cplcv opreq2 eqidd mpteq12dv mpt2eq123dv df-addcv wa oprex pm3.2i
        opreq2d mpt2exga ax-mp fvmpt eqtrd opreqd simp2 simp3 jca fveq1 opreq1d
        3ad2ant1 eqid mptexg ovmpt2 syl ) CUBIZBJKCLMZNMZIZDVMIZUCZBDEMBDGHVMVM
        AVLAOZGOZPZVQHOZPZQMZRZUEZMZAVLVQBPZVQDPZQMZRZVPEWDBDVKVNEWDSVOVKECUFPZ
        WDEWJSVKFUDUACGHJKUAOZLMZNMZWMAWLWBRZUEWDUBUFWKCSZGHWMWMWNVMVMWCWOWLVLJ
        NWKCKLUGZUOZWQWOAWLWBVLWBWPWOWBUHUIUJAHGUAUKVMTIZWRULWDTIWRWRJVLNUMZWSU
        NGHVMVMWCTTUPUQURUSVFUTVPVNVOULWEWISVPVNVOVKVNVOVAVKVNVOVBVCGHBDVMVMWCW
        IWDAVLWFWAQMZRVRBSZAVLWBVLWTXAVLUHXAVSWFWAQVQVRBVDVEUIVTDSZAVLWTVLWHVLV
        LSXBVLVGUDXBWAWGWFQVQVTDVDUOUIWDVGVLTIWITIKCLUMAVLWHTVHUQVIVJUS $.
        $( [14-Sep-2013] $)
    $}

    ${
      $d x U $.  $d x N $.  $d x V $.
      $( Closure of addition of complex vectors.  (Contributed by FL,
         14-Sep-2013.) $)
      claddrv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
        V e. ( CC ^m ( 1 ... N ) ) ) -> ( U +w V ) e. ( CC ^m ( 1 ... N ) ) )
          $=
        ( vx wcel cc c1 cfz co cfv wf wa wi cvv wb elmapg ax-mp ex cn w3a caddc
        cmap cmpt cnex oprex pm3.2i ffvelrn sylbi 3ad2ant2 imp 3ad2ant3 axaddcl
        cv jca syl6 eqid fmptd a1i syl mpbird isaddrv eleq1d ) BUAGZAHIBJKZUDKZ
        GZCVGGZUBZACDKZVGGFVFFUOZALZVLCLZUCKZUEZVGGZVJVQVFHVPMZVJFVFVOHVPVJVLVF
        GZVMHGZVNHGZNZVOHGVJVSWBVJVSNVTWAVJVSVTVHVEVSVTOZVIVHVFHAMZWCHPGZVFPGZN
        ZVHWDQWEWFUFIBJUGUHZHVFAPPRSWDVSVTVFHVLAUITUJUKULVJVSWAVIVEVSWAOZVHVIVF
        HCMZWIWGVIWJQWHHVFCPPRSWJVSWAVFHVLCUITUJUMULUPTVMVNUNUQVPURUSVJWGVQVRQW
        GVJWHUTHVFVPPPRVAVBVJVKVPVGFABCDEVCVDVB $.
        $( [14-Sep-2013] $)
    $}
  $}

  $c 0cv $.

  $( Extends class notation with null vector. $)
  c0cv $a class 0cv $.

  ${
    $d n x $.
    $( The null vector in a space of dimension ` n ` .  Experimental. $)
    df-nullcv $a |- 0cv = ( n e. NN |-> ( x e. ( 1 ... n ) |-> 0 ) ) $.
  $}

  ${
    isnullcv.1 $e |- 0w = ( 0cv ` N ) $.
    ${
      $( The null vector. $)
      $d x n N $.
      $( The null vector in a space of dimension ` n ` .  (Contributed by FL,
         15-Sep-2013.) $)
      isnullcv $p |- ( N e. NN -> 0w = ( x e. ( 1 ... N ) |-> 0 ) ) $=
        ( vn cn wcel c1 cfz co cc0 cmpt cvv wa wceq id oprex mptexg a1i c0cv cv
        ax-mp jca cfv opreq2 eqidd mpteq12dv df-nullcv fvmptg eqtrd syl ) BFGZU
        LAHBIJZKLZMGZNZCUNOULULUOULPUOULUMMGUOHBIQAUMKMRUBSUCUPCBTUDZUNCUQOUPDS
        EBAHEUAZIJZKLUNFMTURBOZAUSKUMKURBHIUEUTKUFUGAEUHUIUJUK $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x N $.
      $( The null vector is a complex vector.  (Contributed by FL,
         15-Sep-2013.) $)
      zernpl $p |- ( N e. NN -> 0w e. ( CC ^m ( 1 ... N ) ) ) $=
        ( vx cn wcel c1 cfz co cc0 cmpt cc cmap isnullcv wf cv a1i12 eqid fmptd
        0cn cnex oprex elmap sylibr eqeltrd ) AEFZBDGAHIZJKZLUGMIZDABCNUFUGLUHO
        UHUIFUFDUGJLUHUFDPUGFJLFTQUHRSLUGUHUAGAHUBUCUDUE $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x A $.  $d x n N $.
      $( Value of the complex vector at a specific coordinate.  (Contributed by
         FL, 15-Sep-2013.) $)
      valvze $p |- ( ( N e. NN /\ A e. ( 1 ... N ) ) -> ( 0w ` A ) = 0 ) $=
        ( vn vx cn wcel c1 cfz co cfv c0cv cc0 cv cmpt wceq a1i fveq1d cvv eqid
        wa fveq1i df-nullcv oprex mptexg opreq2 eqidd mpteq12dv fvmptg mpdan cc
        syl adantr simpr 0cn sylancl 3eqtrd syl5eq ) BGHZAIBJKZHZUBZACLABMLZLZN
        ACVDDUCVCVEABEGFIEOZJKZNPZPZLZLAFVANPZLZNVCAVDVJVCBMVIMVIQVCFEUDRSSVCAV
        JVKUTVJVKQZVBUTVKTHZVMUTVATHZVNVOUTIBJUERFVANTUFUMEBVHVKGTVIVFBQZFVGNVA
        NVFBIJUGVPNUHUIVIUAUJUKUNSVCVBNULHVLNQUTVBUOUPFANNVAULVKFOAQNUHVKUAUJUQ
        URUS $.
        $( [15-Sep-2013] $)
    $}
  $}

  ${
    addcomv.1 $e |- +w = ( +cv ` N ) $.
    ${
      $d x A $.  $d x B $.  $d x N $.
      $( Vector addition is commutative.  (Contributed by FL, 15-Sep-2013.) $)
      addcomv $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) )
      /\ B e. ( CC ^m ( 1 ... N ) ) )
      -> ( A +w B ) = ( B +w A ) ) $=
        ( vx wcel cc c1 cfz co w3a cfv caddc cmpt wa wceq wi wf cnex cn cmap cv
        oprex elmap ffvelrn 3adant1 3adant2 jca 3exp sylbi com12 imp addcom syl
        mpteq2dva isaddrv 3com23 3eqtr4d ) CUAGZAHICJKZUBKZGZBVBGZLZFVAFUCZAMZV
        FBMZNKZOFVAVHVGNKZOZABDKBADKZVEFVAVIVJVEVFVAGZPVGHGZVHHGZPZVIVJQVEVMVPV
        CVDVMVPRZUTVCVDVQVCVAHASZVDVQRHVAATICJUDZUEVDVRVQVDVAHBSZVRVQRHVABTVSUE
        VTVRVMVPVTVRVMLVNVOVRVMVNVTVAHVFAUFUGVTVMVOVRVAHVFBUFUHUIUJUKULUKUMUGUM
        VGVHUNUOUPFACBDEUQUTVDVCVLVKQFBCADEUQURUS $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x y A $.  $d x y B $.  $d x y C $.  $d x y N $.
      $( Vector addition is associative.  (Contributed by FL, 15-Sep-2013.) $)
      addassv $p |- ( ( N e. NN /\ ( A e. ( CC ^m ( 1 ... N ) ) /\
        B e. ( CC ^m ( 1 ... N ) ) /\ C e. ( CC ^m ( 1 ... N ) ) ) ) ->
        ( ( A +w B ) +w C ) = ( A +w ( B +w C ) ) ) $=
        ( vx vy wcel cc co wa cfv caddc wceq wi cvv cnex ffvelrnb mp2an cn cmap
        c1 cfz w3a cv cmpt isaddrv 3adant3r3 opreq1d simpl wf oprex ex anim12ii
        3adant3 adantl axaddcl syl6 eqid wb pm3.2i a1i elmapg syl mpbird simpr3
        fmptd syl3anc simpr1 3ad2ant2 3ad2ant3 jcad 3adant3r1 3ad2ant1 axaddass
        opreq2d imp simpr weq fveq2 opreq12d sylancl 3eqtr4d mpteq2dva 3eqtr4rd
        fvmptg 3eqtrd ) DUAIZAJUCDUDKZUBKZIZBWKIZCWKIZUEZLZABEKZCEKGWJGUFZAMZWR
        BMZNKZUGZCEKZHWJHUFZXBMZXDCMZNKZUGZABCEKZEKZWPWQXBCEWIWLWMWQXBOWNGADBEF
        UHUIUJWPWIXBWKIZWNXCXHOWIWOUKZWPXKWJJXBULZWPGWJXAJXBWPWRWJIZWSJIZWTJIZL
        ZXAJIWOXNXQPZWIWLWMXRWNWLXNXOWMXPWLXNXOWJQIZJQIZWLXNLXOPUCDUDUMZRWJJWRQ
        QASTUNWMXNXPXSXTWMXNLXPPYARWJJWRQQBSTUNZUOUPUQWSWTURUSXBUTZVHWPXTXSLZXK
        XMVAYDWPXTXSRYAVBVCZJWJXBQQVDVEVFWIWLWMWNVGHXBDCEFUHVIWPAGWJWTWRCMZNKZU
        GZEKZHWJXDAMZXDYHMZNKZUGZXJXHWPWIWLYHWKIZYIYMOXLWIWLWMWNVJWPYNWJJYHULZW
        PGWJYGJYHWPXNXPYFJIZLYGJIWPXNXPYPWOXNXPPZWIWMWLYQWNYBVKUQWOXNYPPZWIWNWL
        YRWMWNXNYPXSXTWNXNLYPPYARWJJWRQQCSTUNVLUQVMWTYFURUSYHUTZVHWPYDYNYOVAYEJ
        WJYHQQVDVEVFHADYHEFUHVIWPXIYHAEWIWMWNXIYHOWLGBDCEFUHVNVQWPHWJXGYLWPXDWJ
        IZLZYJXDBMZNKZXFNKZYJUUBXFNKZNKZXGYLUUAYJJIZUUBJIZXFJIZUUDUUFOWPYTUUGWO
        YTUUGPZWIWLWMUUJWNWLYTUUGXSXTWLYTLUUGPYARWJJXDQQASTUNVOUQVRWPYTUUHWOYTU
        UHPZWIWMWLUUKWNWMYTUUHXSXTWMYTLUUHPYARWJJXDQQBSTUNVKUQVRWPYTUUIWOYTUUIP
        ZWIWNWLUULWMWNYTUUIXSXTWNYTLUUIPYARWJJXDQQCSTUNVLUQVRYJUUBXFVPVIUUAYTUU
        CQIZXGUUDOWPYTVSZYJUUBNUMYTUUMLXEUUCXFNGXDXAUUCWJQXBGHVTZWSYJWTUUBNWRXD
        AWAWRXDBWAZWBYCWGUJWCUUAYTUUEQIZYLUUFOUUNUUBXFNUMYTUUQLYKUUEYJNGXDYGUUE
        WJQYHUUOWTUUBYFXFNUUPWRXDCWAWBYSWGVQWCWDWEWFWH $.
        $( [15-Sep-2013] $)
    $}
  $}

  ${
    $d x A a $.  $d x N $.  $d x 0w $.
    addidv2.1 $e |- 0w = ( 0cv ` N ) $.
    addidv2.2 $e |- +w = ( +cv ` N ) $.
    $( The null vector is a left identity for vector addition.  (Contributed by
       FL, 15-Sep-2013.) $)
    addidv2 $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) )
      -> ( 0w +w A ) = A ) $=
      ( vx va wcel cc c1 co wa caddc wceq syl cc0 wi cvv eqtrd cn cfz cmap cmpt
      cv cfv w3a simpl zernpl adantr simpr isaddrv simpll jca valvze opreq1d wf
      3jca wb cnex oprex pm3.2i elmapg ax-mp ffvelrn a1i com12 sylbi imp addid2
      ex mpteq2dva wfn ffn adantl ax-17 dffn5b biimpi eqcomd ) BUAIZAJKBUBLZUCL
      ZIZMZDACLZGWAGUEZDUFZWFAUFZNLZUDZAWDVTDWBIZWCUGWEWJOWDVTWKWCVTWCUHVTWKWCB
      DEUIUJVTWCUKURGDBACFULPWDWJGWAWHUDZAWDGWAWIWHWDWFWAIZMZWIQWHNLZWHWNWGQWHN
      WNVTWMMWGQOWNVTWMVTWCWMUMWDWMUKUNWFBDEUOPUPWNWHJIZWOWHOWDWMWPVTWCWMWPRZWC
      VTWQWCWAJAUQZVTWQRJSIZWASIZMWCWRUSWSWTUTKBUBVAVBJWAASSVCVDZVTWRWQWRWQRVTW
      RWMWPWAJWFAVEVKVFVGVHVGVIVIWHVJPTVLWDAWAVMZWLAOWCXBVTWCWRXBXAWAJAVNVHVOXB
      AWLXBAWLOGWAAHHUEAIGVPVQVRVSPTT $.
      $( [15-Sep-2013] $)

    $( The null vector is a right identity for vector addition.  (Contributed
       by FL, 15-Sep-2013.) $)
    addidrv2 $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) )
      -> ( A +w 0w ) = A ) $=
      ( cn wcel cc c1 cfz co cmap wa w3a wceq simpl simpr zernpl adantr addcomv
      3jca syl addidv2 eqtrd ) BGHZAIJBKLMLZHZNZADCLZDACLZAUIUFUHDUGHZOUJUKPUIU
      FUHULUFUHQUFUHRUFULUHBDESTUBADBCFUAUCABCDEFUDUE $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x y A i $.  $d x y i N $.
    cnegvex2.1 $e |- +w = ( +cv ` N ) $.
    cnegvex2.2 $e |- 0w = ( 0cv ` N ) $.
    cnegvex2.3 $e |- N e. NN $.
    $( Existence of a left inverse for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    cnegvex2 $p |- ( A e. ( CC ^m ( 1 ... N ) ) ->
       E. x e. ( CC ^m ( 1 ... N ) ) ( x +w A ) = 0w ) $=
      ( vi cc co wcel wceq cc0 ci cmul wa ax-mp wi cvv vy c1 cmap cv wrex caddc
      cfz cfv cmpt wf axicn pm3.2i axmulcl a1i wb oprex elmapg ffvelrn ex sylbi
      cnex imp jca syl fmptd mpbird mulid2 opreq2d mulcli ax-1cn adddir mp3an12
      eqid ax-i2m1 mul02 syl5eq eqtr3d mpteq2dva ax-17 hbmpt1 hbeq fveq1 fvmpt2
      opreq1i eqtr com12 com3l opreq1d mpteq2da eqeq1d rcla4ev cplcv cn isaddrv
      3exp ancoms opreq imbi2d c0cv isnullcv id eqeq12d rexbidva ) BJUBCUGKZUCK
      ZLZAUDZBDKZEMZAXEUEIXDIUDZXGUHZXJBUHZUFKZUIZIXDNUIZMZAXEUEZXFIXDOOPKZXLPK
      ZUIZXELZIXDXSXLUFKZUIZXOMZQXQXFYAYDXFYAXDJXTUJZXFIXDXSJXTXFXJXDLZXSJLZXFY
      FQZXRJLZXLJLZQYGYHYIYJYIYHOJLZYKQYIYKYKUKUKULOOUMRUNXFYFYJXFXDJBUJZYFYJSJ
      TLZXDTLZQZXFYLUOYMYNVAUBCUGUPULZJXDBTTUQRYLYFYJXDJXJBURUSUTVBZVCXRXLUMVDU
      SXTVMZVEYAYEUOZXFYOYSYPJXDXTTTUQRUNVFXFIXDYBNYHYJYBNMYQYJXSUBXLPKZUFKZYBN
      YJYTXLXSUFXLVGVHYJXRUBUFKZXLPKZUUANYIUBJLYJUUCUUAMOOUKUKVIVJXRUBXLVKVLYJU
      UCNXLPKNUUBNXLPVNWDXLVOVPVQVQVDVRVCXPYDAXTXEXGXTMZXNYCXOUUDIXDXMYBIUAUAXG
      XTUAUDXGLIVSIUAXDXSVTWAUUDYFQXKXSXLUFUUDYFXKXSMZUUDXKXJXTUHZMZYFUUESZXJXG
      XTWBXSTLZUUGUUHSXRXLPUPYFUUIUUGUUEYFUUIUUGUUESZYFUUIQUUFXSMZUUJIXDXSTXTYR
      WCUUGUUKUUEUUGUUKUUEXKUUFXSWEUSWFVDUSWGRVDVBWHWIWJWKVDXFXIXPAXEXFXGXELZQZ
      XHXNEXODCWLUHZMZUUMXHXNMZSZFUUOUUQUUMXGBUUNKZXNMZSZUUTUUOUULXFUUSUULXFUUS
      CWMLZUULXFUUSSSHUVAUULXFUUSIXGCBUUNUUNVMWNWORVBWPUNUUOUUPUUSUUMUUOXHUURXN
      XGBDUUNWQWJWRVFREXOMZUUMECWSUHZMZUVBGUVDUVBUVCXOMZUVEUVDUVAUVEHICUVCUVCVM
      WTRUNUVDEUVCXOUVDXAWJVFRUNXBXCVF $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x N $.
    cnegvex2b.1 $e |- +w = ( +cv ` N ) $.
    cnegvex2b.2 $e |- 0w = ( 0cv ` N ) $.
    $( Existence of a left inverse for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    cnegvex2b $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) ) ->
       E. x e. ( CC ^m ( 1 ... N ) ) ( x +w A ) = 0w ) $=
      ( cn wcel cc c1 cfz co cmap wceq wrex cplcv cfv c0cv wi wa cv cif opreq2d
      opreq2 eleq2d id fveq2d opreq fveq2 eqeq12d anbi12d rexbidv2 imbi12d eqid
      syl 1nn elimel cnegvex2 dedth imp wb ax-mp eqeq12i rexbii a1i mpbird ) CH
      IZBJKCLMZNMZIZUAZAUBZBDMZEOZAVJPZVMBCQRZMZCSRZOZAVJPZVHVKWAVHVKWATBJKVHCK
      UCZLMZNMZIZVMBWBQRZMZWBSRZOZAWDPZTCKCWBOZVKWEWAWJWKVJWDBWKVIWCJNCWBKLUEUD
      ZUFWKVTWIAVJWDWKVMVJIVMWDIVTWIWKVJWDVMWLUFWKVRWGVSWHWKVQWFOVRWGOWKCWBQWKU
      GUHVMBVQWFUIUPCWBSUJUKULUMUNABWBWFWHWFUOWHUOCKHUQURUSUTVAVPWAVBVLVOVTAVJV
      NVREVSDVQOVNVROFVMBDVQUIVCGVDVEVFVG $.
      $( [15-Sep-2013] $)
  $}

  $c -cv $. $( Substraction of complex vectors. $)

  $( Extends class notation with substraction of complex vectors. $)
  cmcv $a class -cv $.

  ${
    $d n u v w $.
    $( Substraction of complex vectors in a space of dimension ` n ` .
       Experimental. $)
    df-subcv $a |- -cv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) ) ,
        v e. ( CC ^m ( 1 ... n ) ) |->
        ( iota_ w e. ( CC ^m ( 1 ... n ) ) ( v ( +cv ` n ) w ) = u ) ) ) $.
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x N $.  $d x +w $.
    addcanri.1 $e |- +w = ( +cv ` N ) $.
    addcanri.2 $e |- N e. NN $.
    addcanri.3 $e |- A e. ( CC ^m ( 1 ... N ) ) $.
    addcanri.4 $e |- B e. ( CC ^m ( 1 ... N ) ) $.
    addcanri.5 $e |- C e. ( CC ^m ( 1 ... N ) ) $.
    $( Cancellation law for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    addcanri $p |- ( ( A +w B ) = ( A +w C ) <-> B = C ) $=
      ( vx co wceq wcel wi ex w3a wa a1i ax-mp c0cv cfv cc c1 cmap cv wrex eqid
      cfz cnegvex2 pm3.35 cn simpr simpl3 simpl2 3adant3 jca addassv syl opreq2
      3jca 3imp simpl1 eqcomd 3eqtrd 3exp wb opreq1 eqeq12d eqcoms imbi2d com12
      mpbird imp rexlimiva syl6 addidv2 3eqtr3g impbii ) ABELZACELZMZBCMWBDUAUB
      ZBELZWCCELZBCAUCUDDUILUELZNZKUFZAELZWCMZKWFUGZOZWBWDWEMZOZKADEWCFWCUHZGUJ
      WGWLWNOHWGWLWKWNWGWLWKWGWKUKPWJWNKWFWHWFNZWJWNWJWPWNWJWPWNOWPWBWIBELZWICE
      LZMZOZOZXAWJWGXAHBWFNZWGXAOZICWFNZXBXCOJXDXBWGXAXDXBWGQZWPWBWSXEWPWBQZWQW
      HVTELZWHWAELZWRXFDULNZWPWGXBQZRWQXGMXFXIXJXIXFGSZXEWPXJWBXEWPRZWPWGXBXEWP
      UMZXDXBWGWPUNZXDXBWGWPUOVAUPUQWHABDEFURUSXEWPWBXGXHMZWPWBXOOZOXEXPWPVTWAW
      HEUTSSVBXFWRXHXFXIWPWGXDQZRWRXHMXFXIXQXKXEWPXQWBXLWPWGXDXMXNXDXBWGWPVCVAU
      PUQWHACDEFURUSVDVEVFVFTTTSWJWNWTWPWJWMWSWBWMWSVGWCWIWCWIMWDWQWEWRWCWIBEVH
      WCWICEVHVIVJVKVKVMVLVNVOVPTTXBWDBMZIXIXBXROGXIXBXRBDEWCWOFVQPTTXDWECMZJXI
      XDXSOGXIXDXSCDEWCWOFVQPTTVRBCAEUTVS $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x N $.
    addcanrg.1 $e |- +w = ( +cv ` N ) $.
    $( Cancellation law for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    addcanrg $p |- ( ( N e. NN /\
      ( A e. ( CC ^m ( 1 ... N ) ) /\ B e. ( CC ^m ( 1 ... N ) ) /\
        C e. ( CC ^m ( 1 ... N ) ) ) )
     -> ( ( A +w B ) = ( A +w C ) <-> B = C ) ) $=
      ( vx wcel co w3a wa wceq wi cfv simpll jca syl com12 ex imp cn cc c1 cmap
      cfz cplcv c0cv simplr2 eqid addidv2 eqcomd cv wrex simpl simpr1 cnegvex2b
      adantr opreq eqeq1d biimpd opreq2 simplr1 3jca addassv simpr simplr3 eqtr
      simprr opreq1 eqeq12d com3l com4l 3syl com13 pm2.43i com3r syl6 rexlimiva
      eqcoms ax-mp 3eqtrd wb opreqi eqeq12i imbi1i a1i mpbird impbid ) DUAHZAUB
      UCDUEIUDIZHZBWJHZCWJHZJZKZABEIZACEIZLZBCLZWOWRWSMZABDUFNZIZACXAIZLZWSMZWO
      XDWSWOXDKZBDUGNZBXAIZXGCXAIZCXFXHBXFWIWLKXHBLXFWIWLWIWNXDOZWKWLWMWIXDUHPB
      DXAXGXGUIZXAUIZUJQUKXFXHXILZXFWIWKKZGULZAEIZXGLZGWJUMXFXMMZWOXNXDWOWIWKWI
      WNUNWIWKWLWMUOPUQGADEXGFXKUPXQXRGWJXOWJHZXQXRXQXSXREXALZXQXSXRMZMFXTXQXOA
      XAIZXGLZYAXTXQYCXTXPYBXGXOAEXAURUSUTYCXSXRXFYCXSKZXMWOXDYDXMMZXDWOYEXDXOX
      BXAIZXOXCXAIZLZWOYEMXBXCXOXAVAWOYDYHXMWOYDYHXMMZWOYDKZYIYJYFYBBXAIZLZYJYI
      MYJWIXSWKWLJZKZYLYJWIYMWIWNYDOYJXSWKWLWOYCXSVHWKWLWMWIYDVBWKWLWMWIYDUHVCP
      YNYKYFXOABDXAXLVDUKQYJYLYIWOYDYLYIMZYDWOYOYCXSWOYOMWOXSYCYOWOXSYCYOMZWOXS
      KZWIXSWKWMJZKYBCXAIZYGLZYPYQWIYRWIWNXSOYQXSWKWMWOXSVEWKWLWMWIXSVBWKWLWMWI
      XSVFVCPXOACDXAXLVDYHYTYCYLXMYTYCYLXMMMZMYGYFYTYGYFLZUUAYTUUBUUAYTUUBKYSYF
      LZUUAYSYGYFVGYLUUCYCXMUUCYLYCXMMZUUCYLUUDUUCYLKYSYKLUUDYSYFYKVGUUDYKYSYCY
      KYSLZXMYCUUEXMYCYKXHYSXIYBXGBXAVIYBXGCXAVIVJUTRVSQSRVKQSRVSVLVMSVNTRTRQVO
      SVPQRTRSVQVTRTVRVMVOXFWIWMKXICLXFWIWMXJWKWLWMWIXDVFPCDXAXGXKXLUJQWASWTXEW
      BWOWRXDWSWPXBWQXCEXAABFWCEXAACFWCWDWEWFWGWSWRMWOBCAEVAWFWH $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y N $.  $d x y +w $.
    negveud.1 $e |- +w = ( +cv ` N ) $.
    $( Existential uniqueness of vector negatives.  (Contributed by FL,
       15-Sep-2013.) $)
    negveud $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) )
      /\ B e. ( CC ^m ( 1 ... N ) ) )
          -> E! x e. ( CC ^m ( 1 ... N ) ) ( A +w x ) = B ) $=
      ( vy wcel co w3a wceq wa wi jca imp 3adant2 com12 ex eqtr syl cn cfz cmap
      cc c1 cv wreu wrex wral c0cv cfv simp1 simp2 eqid cnegvex2b claddrv com23
      3exp addcomv 3adant3 addidv2 addassv eqcomd 3expd com34 3imp opreq1 com4l
      3ad2ant3 3ad2ant2 com3l pm2.43i opreq2 eqeq1d rexlimiv 3syl simpl1 simpl2
      rcla4ev simprl simprr 3jca addcanrg biimpd com3r eqcoms ralrimivv wb reu4
      a1i mpbird ) DUAHZBUDUEDUBIUCIZHZCWMHZJZBAUFZEIZCKZAWMUGZWSAWMUHZWSBGUFZE
      IZCKZLZWQXBKZMZGWMUIAWMUIZLZWPXAXHWPXAWPWLWNLXBBEIZDUJUKZKZGWMUHWPXAMZWPW
      LWNWLWNWOULWLWNWOUMNGBDEXKFXKUNZUOXLXMGWMXBWMHZXLWPXAXOXLWPJZXBCEIZWMHZBX
      QEIZCKZLXAXPXRXTXOWPXRXLXOWPXRWPXOXRWLWOXOXRMZWNWLWOYAWLXOWOXRWLXOWOXRXBD
      CEFUPURUQOPQOPXPXTXPXCXJKZXPXTMXOWPYBXLXOWPYBWPXOYBWLWNXOYBMZWOWLWNYCWLWN
      XOYBBXBDEFUSUROUTQOPXPYBXTXPYBXTMZXPXPYDMZXPXKCEIZCKZXPYEMWPXOYGXLWLWOYGW
      NCDEXKXNFVAPVIXPYGYEXPXSXCCEIZKZYGYEMXOWPYIXLXOWPYIWPXOYIWLWNWOXOYIMWLWNX
      OWOYIWLWNXOWOYIWLWNXOWOJZYIWLYJLYHXSBXBCDEFVBVCRVDVEVFQOPXPYIYGYDXLXOYIYG
      YDMMWPYBXLYIYGXTYBXLYIYGXTMZMZYBXLLXCXKKZYLXCXJXKSYMYHYFKZYLXCXKCEVGYIYNY
      KYIYNYKYIYNLXSYFKZYKXSYHYFSYOYGXTXSYFCSRTRQTTRVHVJVKTQTVLVLQTVLNWSXTAXQWM
      WQXQKWRXSCWQXQBEVMVNVSTURVOVPVLWPXGAGWMWMXEWPWQWMHZXOLZXFWSXDWPYQXFMMZXDW
      SYRWSYRMCXCWSCXCKZYRWSYSYRWSYSLWRXCKZYRWRCXCSWPYQYTXFWPYQYTXFMZWPYQLZWLWN
      YPXOJZLZUUAUUBWLUUCWLWNWOYQVQUUBWNYPXOWLWNWOYQVRWPYPXOVTWPYPXOWAWBNUUDYTX
      FBWQXBDEFWCWDTRWETRQWFQOVKWGNWTXIWHWPWSXDAGWMXFWRXCCWQXBBEVMVNWIWJWK $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d w v u U $.  $d w v u n N $.  $d w v V $.  $d w +w $.
    issubcv.1 $e |- +w = ( +cv ` N ) $.
    issubcv.2 $e |- -w = ( -cv ` N ) $.
    $( Substraction of complex vectors in a space of dimension ` n ` .
       (Contributed by FL, 15-Sep-2013.) $)
    issubcv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
                       V e. ( CC ^m ( 1 ... N ) ) )
     -> ( U -w V ) =
       ( iota_ w e. ( CC ^m ( 1 ... N ) ) ( V +w w ) = U ) ) $=
      ( vu vv wcel cc co cmap wceq crio wi cvv ax-mp eqeq1d vn cn c1 cfz w3a cv
      cmcv cfv cplcv cmpt2 wa oprex pm3.2i mpt2exga opreq2 opreq2d fveq2 opreqd
      riotaeqbidv mpt2eq123dv df-subcv fvmptg ex com12 riotaex riotabidv opreq1
      eqeq2 eqid ovmpt2g 3exp com3r a1i opreq imbi2d mpbird syl 3imp riotabidva
      wb adantr eqeq2d bitrd ) CUBKZBLUCCUDMZNMZKZDWFKZUEZBDFMZDAUFZEMZBOZAWFPZ
      OZBDCUGUHZMZDWKCUIUHZMZBOZAWFPZOZWDWGWHXBWDWPIJWFWFJUFZWKWRMZIUFZOZAWFPZU
      JZOZWGWHXBQZQZXHRKZWDXIQWFRKZXMUKXLXMXMLWENULZXNUMIJWFWFXGRRUNSWDXLXIWDXL
      XIUACIJLUCUAUFZUDMZNMZXQXCWKXOUIUHZMZXEOZAXQPZUJXHUBRUGXOCOZIJXQXQYAWFWFX
      GYBXPWELNXOCUCUDUOUPZYCYBXTXFAXQWFYCYBXSXDXEYBXRWRXCWKXOCUIUQURTUSUTAJIUA
      VAVBVCVDSXIXKWGWHBDXHMZXAOZQZQZYGXIXARKZYGWTAWFVEWGWHYHYEWGWHYHYEIJBDWFWF
      XGXAXHXDBOZAWFPRXEBOXFYIAWFXEBXDVHVFXCDOZYIWTAWFYJXDWSBXCDWKWRVGTVFXHVIVJ
      VKVLSVMXIXJYFWGXIXBYEWHXIWQYDXABDWPXHVNTVOVOVPVQVRWOXBVTZWIEWROZYKGYLWOWJ
      XAOZXBYLWNXAWJYLWMWTAWFYLWKWFKZUKWLWSBYLWLWSOYNDWKEWRVNWATVSWBYMXBVTZYLFW
      POZYOHYPWJWQXABDFWPVNTSVMWCSVMVP $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x U $.  $d x N $.  $d x V $.
    subclcvd.1 $e |- -w = ( -cv ` N ) $.
    $( Closure law for vector substraction.  (Contributed by FL,
       15-Sep-2013.) $)
    subclcvd $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
       V e. ( CC ^m ( 1 ... N ) ) )
       -> ( U -w V ) e. ( CC ^m ( 1 ... N ) ) ) $=
      ( vx cn wcel cc c1 cfz co cmap w3a cv cplcv cfv wceq crio eqid wreu com23
      issubcv negveud 3exp 3imp wb oprex riotaclb a1i bicomd mpbird eqeltrd ) B
      GHZAIJBKLZMLZHZCUPHZNZACDLCFOBPQZLARZFUPSZUPFABCUTDUTTZEUCUSVBUPHZVAFUPUA
      ZUNUQURVEUNURUQVEUNURUQVEFCABUTVCUDUEUBUFUSVEVDVEVDUGUSVAFUPIUOMUHUIUJUKU
      LUM $.
      $( [15-Sep-2013] $)
  $}

  $c -ucv $. $( Negative of a complex vector. $)

  $( Extends class notation with the negative of a complex vector. $)
  cnegcv $a class -ucv $.

  ${
    $d n u $.
    $( Negative of a complex vector.  Experimental. $)
    df-ucv $a |- -ucv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) )
      |-> ( ( 0cv ` n ) ( -cv ` n ) u ) ) ) $.
  $}

  ${
    $d u U $.  $d u n N $.
    isucv.1 $e |- ~w = ( -ucv ` N ) $.
    isucv.2 $e |- 0w = ( 0cv ` N ) $.
    isucv.3 $e |- -w = ( -cv ` N ) $.
    $( Negative of a complex vector.  (Contributed by FL, 15-Sep-2013.) $)
    isucv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) ) ->
       ( ~w ` U ) = ( 0w -w U ) ) $=
      ( vu vn cfv wceq wcel cc co cmap wi cvv ax-mp a1i cnegcv cn cfz c0cv cmcv
      c1 wa cv cmpt simpl oprex mptexg jca opreq2 opreq2d fveq2 eqidd opreq123d
      mpteq12dv df-ucv fvmptg syl fveq1d simpr eqtrd opreq eqeq2d imbi2d mpbird
      eqid opreq1 fveq1 eqeq1d ) EBUAKZLZBUBMZANUFBUCOZPOZMZUGZAEKZDACOZLZQZFVO
      WDVTAVNKZWBLZQZWGVODBUDKZLZWGGWIWGVTWEWHACOZLZQZWLWICBUEKZLZWLHWNWLVTWEWH
      AWMOZLZQZWQWNVTWEAIVRWHIUHZWMOZUIZKZWOVTAVNWTVTVPWTRMZUGVNWTLVTVPXBVPVSUJ
      XBVTVRRMXBNVQPUKIVRWSRULSTUMJBINUFJUHZUCOZPOZXCUDKZWRXCUEKZOZUIWTUBRUAXCB
      LZIXEXHVRWSXIXDVQNPXCBUFUCUNUOXIXFWHWRWRXGWMXCBUEUPXCBUDUPXIWRUQURUSIJUTV
      AVBVCVTVSWORMZUGXAWOLVTVSXJVPVSVDXJVTWHAWMUKTUMIAWSWOVRRWTWRAWHWMUNWTVJVA
      VBVETWNWKWPVTWNWJWOWEWHACWMVFVGVHVISTWIWFWKVTWIWBWJWEDWHACVKVGVHVISTVOWCW
      FVTVOWAWEWBAEVNVLVMVHVIS $.
      $( [15-Sep-2013] $)
  $}

  $c .cv $. $( Multiplication of complex vectors by a scalar. $)

  $( Extends class notation with scalar multiplication of complex vectors. $)
  csmcv $a class .cv $.

  ${
    $d n s u x $.
    $( Multiplication of complex vectors by a scalar in a space of dimension
       ` n ` .  Experimental. $)
    df-mulcv $a |- .cv = ( n e. NN |->
      ( s e. CC , u e. ( CC ^m ( 1 ... n ) ) |->
        ( x e. ( 1 ... n ) |-> ( s x. ( u ` x ) ) ) ) ) $.
  $}

  ${
    $d x u S s $.  $d x u U $.  $d x u n N s $.
    ismulcv.1 $e |- .t = ( .cv ` N ) $.
    $( Multiplication of complex vectors by a scalar in a space of dimension
       ` n ` .  (Contributed by FL, 15-Sep-2013.) $)
    ismulcv $p |- ( ( N e. NN /\ S e. CC /\ U e. ( CC ^m ( 1 ... N ) ) )
       -> ( S .t U ) = ( x e. ( 1 ... N ) |-> ( S x. ( U ` x ) ) ) ) $=
      ( vs vu vn wceq wcel cc c1 cfz co cmap cv cmul cmpt cvv csmcv cn wi cmpt2
      cfv w3a wa simp1 oprex pm3.2i mpt2exga ax-mp a1i jca eqidd opreq2 opreq2d
      cnex mpteq12dv mpt2eq123dv df-mulcv fvmptg opreqd simp2 simp3 mptexg 3jca
      syl opreq1 adantr mpteq2dva fveq1 eqid ovmpt2g eqtrd eqeq1d imbi2d mpbird
      opreq ) EDUAUEZJZDUBKZBLKZCLMDNOZPOZKZUFZBCEOZAWDBAQZCUEZROZSZJZUCZFWAWNW
      GBCVTOZWLJZUCZWQWAWGWOBCGHLWEAWDGQZWIHQZUEZROZSZUDZOZWLWGVTXCBCWGWBXCTKZU
      GVTXCJWGWBXEWBWCWFUHXEWGLTKZWETKZUGXEXFXGURLWDPUIUJGHLWEXBTTUKULUMUNIDGHL
      LMIQZNOZPOZAXIXASZUDXCUBTUAXHDJZGHLXJXKLWEXBXLLUOXLXIWDLPXHDMNUPZUQXLAXIX
      AWDXAXMXLXAUOUSUTAHIGVAVBVHVCWGWCWFWLTKZUFXDWLJWGWCWFXNWBWCWFVDWBWCWFVEXN
      WGWDTKXNMDNUIAWDWKTVFULUMVGGHBCLWEXBWLXCAWDBWTROZSTWRBJZAWDXAXOXPXAXOJWIW
      DKZWRBWTRVIVJVKWSCJZAWDXOWKXRXOWKJXQXRWTWJBRWIWSCVLUQVJVKXCVMVNVHVOUMWAWM
      WPWGWAWHWOWLBCEVTVSVPVQVRUL $.
      $( [15-Sep-2013] $)
  $}

  $c /cv $. $( Division of complex vectors by a scalar. $)

  $( Extends class notation with scalar division of complex vectors. $)
  cdivcv $a class /cv $.

  ${
    $d n u v s $.
    $( Division of complex vectors by a scalar in a space of dimension ` n ` .
       Experimental. $)
    df-divcv $a |- /cv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) ) ,
        s e. CC |->
        ( iota_ v e. ( CC ^m ( 1 ... n ) ) ( s ( .cv ` n ) v ) = u ) ) ) $.
  $}

  ${
    $d v u U s $.  $d v S s $.  $d v u n N s $.  $d v .t $.
    isdivcv.1 $e |- /t = ( /cv ` N ) $.
    isdivcv.2 $e |- .t = ( .cv ` N ) $.
    $( Division of complex vectors by a scalar in a space of dimension ` n ` .
       (Contributed by FL, 15-Sep-2013.) $)
    isdivcv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\ S e. CC ) ->
      ( U /t S ) = ( iota_ v e. ( CC ^m ( 1 ... N ) ) ( S .t v ) = U ) ) $=
      ( vu vs wceq wcel cc co cmap cv crio cvv a1i eqeq1d vn cdivcv cfv cfz w3a
      cn c1 wi csmcv cmpt2 wa simp1 oprex cnex pm3.2i mpt2exga ax-mp jca opreq2
      opreq2d eqidd fveq2 opreqd riotaeqbidv mpt2eq123dv fvmptg syl simp2 simp3
      df-divcv riotaex eqeq2 riotabidv opreq1 ovmpt2g eqtrd opreq eqeq2d imbi2d
      3jca eqid mpbird ) FDUBUCZKZDUFLZCMUGDUDNZONZLZBMLZUEZCBFNZBAPZENZCKZAWGQ
      ZKZUHZGWDWQWJCBWCNZWOKZUHZWTWDEDUIUCZKZWTHXBWTWJWRBWLXANZCKZAWGQZKZUHZXGX
      BWJWRCBIJWGMJPZWLXANZIPZKZAWGQZUJZNZXEWJWCXMCBWJWEXMRLZUKWCXMKWJWEXOWEWHW
      IULXOWJWGRLZMRLZUKXOXPXQMWFOUMUNUOIJWGMXLRRUPUQSURUADIJMUGUAPZUDNZONZMXHW
      LXRUIUCZNZXJKZAXTQZUJXMUFRUBXRDKZIJXTMYDWGMXLYEXSWFMOXRDUGUDUSUTZYEMVAYEY
      CXKAXTWGYFYEYBXIXJYEYAXAXHWLXRDUIVBVCTVDVEAIUAJVJVFVGVCWJWHWIXERLZUEXNXEK
      WJWHWIYGWEWHWIVHWEWHWIVIYGWJXDAWGVKSVTIJCBWGMXLXEXMXICKZAWGQRXJCKXKYHAWGX
      JCXIVLVMXHBKZYHXDAWGYIXIXCCXHBWLXAVNTVMXMWAVOVGVPSXBWSXFWJXBWOXEWRXBWNXDA
      WGXBWMXCCBWLEXAVQTVMVRVSWBUQSWDWPWSWJWDWKWRWOCBFWCVQTVSWBUQ $.
      $( [15-Sep-2013] $)
  $}

