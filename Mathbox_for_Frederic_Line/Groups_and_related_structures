$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Groups and related structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d G x $.  $d U x $.  $d V x $.  $d X x $.
    $( If a magma has a left identity element and a right identity element,
       they are equal.  (Contributed by FL, 25-Sep-2011.) $)
    ridlideq $p |- ( ( U e. X /\ V e. X ) ->
      ( A. x e. X ( ( U G x ) = x /\ ( x G V ) = x )
        -> U = V ) ) $=
      ( cv co wceq wa wral wcel wi oveq2 eqeq12d oveq1 anbi12d rcla4va im2anan9
      id ex eqtr eqcoms adantrd adantl imp syl6com anidms com12 ) BAFZCGZUIHZUI
      DCGZUIHZIZAEJZBEKZDEKZIZBDHZUOURUSLURUOUOIBBCGZBHZBDCGZBHZIZVBDHZDDCGZDHZ
      IZIUSUPUOVDUQUOVHUPUOVDUNVDABEUIBHZUKVAUMVCVIUJUTUIBUIBBCMVISZNVIULVBUIBU
      IBDCOVJNPQTUQUOVHUNVHADEUIDHZUKVEUMVGVKUJVBUIDUIDBCMVKSZNVKULVFUIDUIDDCOV
      LNPQTRVDVHUSVCVHUSLVAVCVEUSVGVEUSLBVBBVBHVEUSBVBDUATUBUCUDUEUFUGUH $.
      $( [25-Sep-2011] $)
  $}

  ${
    $d G x $.  $d U x $.  $d V x $.  $d X x $.
    $( If a magma has a left zero element and a right zero element, they are
       equal.  (Contributed by FL, 25-Dec-2011.) $)
    rzrlzreq $p |- ( ( U e. X /\ V e. X ) ->
      ( A. x e. X ( ( U G x ) = U /\ ( x G V ) = V )
        -> U = V ) ) $=
      ( cv co wceq wa wral wcel wi oveq2 eqeq1d oveq1 anbi12d rcla4va ex com12
      eqtr eqcoms adantr adantl imp syl2an an4s expcom anidms ) BAFZCGZBHZUIDCG
      ZDHZIZAEJZBEKZDEKZIZBDHZUOURUSLURUOUOIUSUPUOUQUOUSUPUOIBBCGZBHZBDCGZDHZIZ
      VBBHZDDCGZDHZIZUSUQUOIUNVDABEUIBHZUKVAUMVCVIUJUTBUIBBCMNVIULVBDUIBDCONPQU
      NVHADEUIDHZUKVEUMVGVJUJVBBUIDBCMNVJULVFDUIDDCONPQVDVHUSVCVHUSLVAVHVCUSVEV
      CUSLZVGVKBVBBVBHVCUSBVBDTRUAUBSUCUDUEUFUGUHS $.
      $( [25-Dec-2011] $)
  $}


  ${
    $d G x y z $.  $d U x y z $.  $d X x y z $.
    mgmlion.1 $e |- X = dom dom G $.
    $( If a magma has a left identity element, it is onto.  (Contributed by FL,
       25-Sep-2011.) $)
    mgmlion $p |- ( ( G e. Magma /\ U e. X /\ A. x e. X ( U G x ) = x )
      -> G : ( X X. X ) -onto-> X ) $=
      ( vy vz cmagm wcel cv co wceq wral w3a cxp wf wrex wa eqeq2d rcla4ev syl6
      wfo ismgm ibi 3ad2ant1 simpllr simplr eqcoms adantl weq oveq2 syl2anc jca
      id ex ax-17 oveq1 rexbid ralimdva 3impia foov sylanbrc ) CHIZBDIZBAJZCKZV
      ELZADMZNDDOZDCPZVEFJZGJZCKZLZGDQZFDQZADMZVIDCUBVCVDVJVHVCVJHCDEUCUDUEVCVD
      VHVQVCVDRZVGVPADVRVEDIZRZVGVDVEBVLCKZLZGDQZRZVPVTVGWDVTVGRZVDWCVCVDVSVGUF
      WEVSVEVFLZWCVRVSVGUGVGWFVTWFVEVFWFUNUHUIWBWFGVEDGAUJWAVFVEVLVEBCUKSTULUMU
      OVOWCFBDVKBLZVNWBGDWGGUPWGVMWAVEVKBVLCUQSURTUAUSUTFGADDDCVAVB $.
      $( [25-Sep-2011] $)
  $}

  $( ` RR ` is a group for addition.  (Contributed by FL, 22-Dec-2008.) $)
  rrisgrp $p |- ( + |` ( RR X. RR ) ) e. GrpOp $=
    ( caddc cgr wcel cr cxp cres wss csubg cfv w3a readdsubg issubg mpbi simp2i
    ) ABCZADDEFZBCZPAGZPAHICOQRJKAPLMN $.
    $( [22-Dec-2008] $)

  $( A way to express the domain of a group.  (Contributed by FL,
     9-Jan-2011.) $)
  dmrngrp $p |- ( G e. GrpOp -> dom G = ( ran G X. ran G ) ) $=
    ( cgr wcel crn cxp wfo cdm wceq eqid grpofo wf fof fdm syl ) ABCADZOEZOAFZA
    GPHZAOOIJQPOAKRPOALPOAMNN $.
    $( [9-Jan-2011] $)

  ${
    $d G x y $.  $d X x y $.
    bsmgrli.1 $e |- X = ran G $.
    $( The base set of an operation with a right and left identity element is
       not empty.  (Contributed by FL, 18-Feb-2010.) $)
    bsmgrli $p |- ( G e. ( Magma i^i ExId ) -> X =/= (/) ) $=
      ( vy vx cmagm cexid cin wcel cv co wceq wa wral wrex c0 wne isexid2 rexn0
      syl ) AFGHIDJZEJZAKUBLUBUAAKUBLMEBNZDBOBPQEDABCRUCDBST $.
      $( [23-Dec-2013] $) $( [18-Feb-2010] $)
  $}

  ${
    $d A x y z $.  $d B y z $.  $d C z $.  $d G x y z $.  $d X x y z $.
    smgrpass2.1 $e |- X = dom dom G $.
    $( A semi-group is associative.  (Contributed by FL, 12-Dec-2009.) $)
    smgrpass2 $p |- ( ( G e. SemiGrp /\ ( A e. X /\ B e. X /\ C e. X ) )
      -> ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( vx vy vz csem wcel cv co wceq wral oveq1 oveq1d eqeq12d oveq2 oveq2d
      w3a smgrpass rcla43v mpan9 ) DJKGLZHLZDMZILZDMZUEUFUHDMZDMZNZIEOHEOGEOAEK
      BEKCEKUAABDMZCDMZABCDMZDMZNZGHIDEFUBULUQAUFDMZUHDMZAUJDMZNUMUHDMZABUHDMZD
      MZNGHIABCEEEUEANZUIUSUKUTVDUGURUHDUEAUFDPQUEAUJDPRUFBNZUSVAUTVCVEURUMUHDU
      FBADSQVEUJVBADUFBUHDPTRUHCNZVAUNVCUPUHCUMDSVFVBUOADUHCBDSTRUCUD $.
      $( [12-Dec-2009] $)
  $}

  ${
    $d G x y $.
    $( An abelian group is a commutative group.  (Contributed by FL,
       14-Sep-2010.) $)
    ablcomgrp $p |- ( G e. AbelOp -> G e. ( GrpOp i^i Com1 ) ) $=
      ( vx vy cgr wcel cv co wceq crn wral wa ccm1 cabl cin grporndm eqid iscom
      cdm wi biimprd eleq2 raleq imbi12d ralbidv2 imbi1d mpcom imdistani isablo
      syl5ibr elin 3imtr4i ) ADEZBFZCFZAGUNUMAGHZCAIZJZBUPJZKULALEZKAMEADLNEULU
      RUSUPARRZHZULURUSSZAOULVBVAUOCUTJZBUTJZUSSULUSVDBCDAUTUTPQTVAURVDUSVAUQVC
      BUPUTVAUMUPEUMUTEUQVCUPUTUMUAUOCUPUTUBUCUDUEUIUFUGBCAUPUPPUHADLUJUK $.
      $( [14-Sep-2010] $)
  $}


  ${
    reacomsmgrp1.1 $e |- X = dom dom G $.
    $( Rearrangement of three terms in a commutative semi-group.  (Contributed
       by FL, 14-Sep-2010.) $)
    reacomsmgrp1 $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
      ( A e. X /\ B e. X /\ C e. X ) )
        -> ( A G ( B G C ) ) = ( B G ( A G C ) ) ) $=
      ( csem ccm1 cin wcel w3a wa wceq inss1 sseli smgrpass2 eqcomd sylan inss2
      co adantr simpr1 simpr2 iscomb syl3anc oveq1d simp2 simp1 syl2an 3eqtrd
      simp3 3jca ) DGHIZJZAEJZBEJZCEJZKZLZABCDTDTZABDTZCDTZBADTZCDTZBACDTDTZUND
      GJZURUTVBMUMGDGHNOZVFURLVBUTABCDEFPQRUSVAVCCDUSDHJZUOUPVAVCMUNVHURUMHDGHS
      OUAUNUOUPUQUBUNUOUPUQUCABDEFUDUEUFUNVFUPUOUQKVDVEMURVGURUPUOUQUOUPUQUGUOU
      PUQUHUOUPUQUKULBACDEFPUIUJ $.
      $( [14-Sep-2010] $)


    $( Rearrangement of three terms in a commutative semi-group.  (Contributed
       by FL, 14-Sep-2010.) $)
    reacomsmgrp2 $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
      ( A e. X /\ B e. X /\ C e. X ) )
        -> ( A G ( B G C ) ) = ( C G ( B G A ) ) ) $=
      ( csem ccm1 cin wcel w3a wa co wceq inss1 smgrpass2 eqcomd iscomb syl3anc
      sseli sylan inss2 adantr simpr1 simpr2 oveq1d cmagm smgrpismgm clmgm 3exp
      syl wi com13 imp 3adant3 mpan9 simpr3 3eqtrd ) DGHIZJZAEJZBEJZCEJZKZLZABC
      DMDMZABDMZCDMZBADMZCDMZCVIDMZUTDGJZVDVFVHNUSGDGHOTZVLVDLVHVFABCDEFPQUAVEV
      GVICDVEDHJZVAVBVGVINUTVNVDUSHDGHUBTUCZUTVAVBVCUDUTVAVBVCUEABDEFRSUFVEVNVI
      EJZVCVJVKNVOUTDUGJZVDVPUTVLVQVMDUHUKVAVBVQVPULZVCVAVBVRVQVBVAVPVQVBVAVPBA
      DEFUIUJUMUNUOUPUTVAVBVCUQVICDEFRSUR $.
      $( [14-Sep-2010] $)


    $( Rearrangement of three terms in a commutative semi-group.  (Contributed
       by FL, 14-Sep-2010.) $)
    reacomsmgrp3 $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
      ( A e. X /\ B e. X /\ C e. X ) )
        -> ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( csem ccm1 cin wcel w3a wa wceq inss2 sseli adantr clmgm syl3anc iscomb
      co cmagm inss1 smgrpismgm syl simpr1 simpr2 simpr3 simp3 simp1 simp2 3jca
      reacomsmgrp2 sylan2 3eqtrd ) DGHIZJZAEJZBEJZCEJZKZLZABDTZCDTZCVBDTZBACDTZ
      DTZVEBDTZVADHJZVBEJZUSVCVDMUPVHUTUOHDGHNOPZVADUAJZUQURVIUPVKUTUPDGJVKUOGD
      GHUBODUCUDPZUPUQURUSUEZUPUQURUSUFZABDEFQRUPUQURUSUGZVBCDEFSRUTUPUSUQURKVD
      VFMUTUSUQURUQURUSUHUQURUSUIUQURUSUJUKCABDEFULUMVAVHURVEEJZVFVGMVJVNVAVKUQ
      USVPVLVMVOACDEFQRBVEDEFSRUN $.
      $( [14-Sep-2010] $)


    $( Rearrangement of terms in a commutative semi-group.  (Contributed by FL,
       18-Sep-2010.) $)
    reacomsmgrp4 $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
      ( A e. X /\ B e. X /\ C e. X ) )
        -> ( A G ( B G C ) ) = ( ( C G B ) G A ) ) $=
      ( csem ccm1 cin wcel w3a wa co reacomsmgrp2 inss1 sseli simp3 simp2 simp1
      wceq 3jca smgrpass2 eqcomd syl2an eqtrd ) DGHIZJZAEJZBEJZCEJZKZLABCDMDMCB
      ADMDMZCBDMADMZABCDEFNUGDGJZUJUIUHKZULUMTUKUFGDGHOPUKUJUIUHUHUIUJQUHUIUJRU
      HUIUJSUAUNUOLUMULCBADEFUBUCUDUE $.
      $( [18-Sep-2010] $)

  $}

  ${
    $d m x y A $.  $d x y G $.  $d k m x y M $.  $d k m x y N $.  $d k x y X $.
    clfsebs.1 $e |- X = dom dom G $.
    $( Closure of a finite composite of elements of the base set of an internal
       operation.  (Contributed by FL, 14-Sep-2010.)  (The proof was shortened
       by Mario Carneiro, 26-May-2014.) $)
    clfsebs $p |- ( ( N e. ( ZZ>= ` M ) /\ G e. Magma /\
                      A. k e. ( M ... N ) A e. X )
       -> prod_ k e. ( M ... N ) G A e. X ) $=
      ( vm vx vy cuz cfv wcel cmagm cfz co wral cprd cv wceq w3a cmpt cseq ssid
      wss eqid prodeqfv ax-mp fprodser 3ad2ant1 syl5eqr simp1 wf ffvelrn sylanb
      fmpt 3ad2antl3 wa clmgm 3expb 3ad2antl2 seqcl eqeltrd ) EDKLMZCNMZAFMBDEO
      PZQZUAZVFABCRZECBVFAUBZDUCLZFVHVIVFHSVJLHCRZVKVFVFUEVLVITVFUDAVFVFBHVJCVJ
      UFZUGUHVDVEVLVKTVGHVJCDEUIUJUKVHIJCFVJDEVDVEVGULVGVDISZVFMZVNVJLFMZVEVGVF
      FVJUMVOVPBVFFAVJVMUPVFFVNVJUNUOUQVEVDVNFMZJSZFMZURVNVRCPFMZVGVEVQVSVTVNVR
      CFGUSUTVAVBVC $.
      $( [26-May-2014] $) $( [14-Sep-2010] $)
  $}

  ${
    $d G k $.  $d M k $.  $d N k $.
    $( Closure of a finite composite of elements of the base set of an internal
       operation.  (Closed version.)  (Contributed by FL, 14-Sep-2010.) $)
    clfsebsg $p |- ( ( N e. ( ZZ>= ` M ) /\ G e. Magma /\
                      A. k e. ( M ... N ) A e. dom dom G )
       -> prod_ k e. ( M ... N ) G A e. dom dom G ) $=
      ( cdm eqid clfsebs ) ABCDECFFZIGH $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d G k $.  $d M k $.  $d N k $.
    clfsebsr.1 $e |- X = ran G $.
    $( Closure of a finite composite of elements of the base set of an internal
       operation.  (Case of a magma with a right and left identity element.)
       (Contributed by FL, 14-Sep-2010.) $)
    clfsebsr $p |- ( ( N e. ( ZZ>= ` M ) /\ G e. ( Magma i^i ExId ) /\
                      A. k e. ( M ... N ) A e. X )
       -> prod_ k e. ( M ... N ) G A e. X ) $=
      ( cuz cfv wcel cmagm cexid cin cfz co wral cdm 3ad2ant2 wi wceq w3a simp1
      cprd inss1 crn rngopid eqcomi eqeq1i biimpi eleq2d ralbidv biimpd syl a1i
      sseli 3imp clfsebsg syl3anc syl5eq eleqtrrd ) EDHIJZCKLMZJZAFJZBDENOZPZUA
      ZVEABCUCZCQQZFVGVACKJZAVIJZBVEPZVHVIJVAVCVFUBVCVAVJVFVBKCKLUDUORVAVCVFVLV
      CVFVLSZSVAVCCUEZVITZVMCUFZVOVFVLVOVDVKBVEVOFVIAVOFVITZVNFVIFVNGUGUHUIUJUK
      ULUMUNUPABCDEUQURVCVAVQVFVCFVNVIGVPUSRUT $.
      $( [14-Sep-2010] $)
  $}


  ${
    $d G k $.  $d M k $.  $d N k $.  $d X k $.
    clfsebs2.1 $e |- X = ran G $.
    $d G k $.  $d M k $.  $d N k $.
    $( Closure of a finite composite of elements of the base set of an internal
       operation (when the operation has an identity element).  (Contributed by
       FL, 14-Sep-2010.) $)
    clfsebs2 $p |- ( ( N e. ( ZZ>= ` M ) /\ G e. ( Magma i^i ExId ) /\
                      A. k e. ( M ... N ) A e. X )
       -> prod_ k e. ( M ... N ) G A e. X ) $=
      ( cuz cfv wcel cmagm cexid cin cfz wral w3a wi wceq cdm eleq2 co cprd crn
      rngopid wa eqtr inss1 eqid clfsebs syl3an2 ralbidv 3anbi3d imbi12d mpbiri
      sseli syl sylancr 3ad2ant2 pm2.43i ) EDHIJZCKLMZJZAFJZBDENUAZOZPZVDABCUBZ
      FJZVBUTVFVHQZVEVBFCUCZRZVJCSSZRZVIGCUDVKVMUEFVLRZVIFVJVLUFVNVIUTVBAVLJZBV
      DOZPZVGVLJZQVBUTCKJVPVRVAKCKLUGUOABCDEVLVLUHUIUJVNVFVQVHVRVNVEVPUTVBVNVCV
      OBVDFVLATUKULFVLVGTUMUNUPUQURUS $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d x y G $.  $d k m x y M $.  $d k m x y N $.  $d k m x y U $.
    fincmpzer.1 $e |- U = ( Id ` G ) $.
    $( Finite composite of identity elements.  (Contributed by FL,
       14-Sep-2010.)  (The proof was shortened by Mario Carneiro,
       26-May-2014.) $)
    fincmpzer $p |- ( ( N e. ( ZZ>= ` M ) /\ G e. ( Magma i^i ExId ) )
       -> prod_ k e. ( M ... N ) G U = U ) $=
      ( vm vx vy cfv wcel wa co cprd wceq cv eqid cgi fvex elsnc cuz cmagm cmpt
      cexid cin cfz cseq wss prodeqfv ax-mp fprodser syl5eqr adantr simpl eqidd
      ssid csn cvv eqeltri fvmpt adantl sylibr elsn anbi12i oveq12 crn cmpidelt
      iorlid mpdan simpld sylan9eqr ovex sylan2b adantll seqcl elsni syl eqtrd
      ) EDUAJKZCUBUDUEKZLZDEUFMZABCNZECBWBAUCZDUGJZAVSWCWEOVTVSWCWBGPWDJGCNZWEW
      BWBUHWFWCOWBUPAWBWBBGWDCWDQZUIUJGWDCDEUKULUMWAWEAUQZKWEAOWAHICWHWDDEVSVTU
      NWAHPZWBKZLWIWDJZAOZWKWHKWJWLWABWIAAWBWDBPWIOAUOWGACRJURFCRSUSUTVAWKAWIWD
      STVBVTWIWHKZIPZWHKZLZWIWNCMZWHKZVSWPVTWIAOZWNAOZLZWRWMWSWOWTHAVCIAVCVDVTX
      ALWQAOWRXAVTWQAACMZAWIAWNACVEVTXBAOZXCVTACVFZKXCXCLACXDXDQZFVHAACXDXEFVGV
      IVJVKWQAWIWNCVLTVBVMVNVOWEAVPVQVR $.
      $( [26-May-2014] $) $( [14-Sep-2010] $)
  $}

  ${
    resgcom.1 $e |- X = dom dom G $.
    $( Rearrangement of four terms in a commutative, associative magma.
       (Contributed by FL, 14-Sep-2010.) $)
    resgcom $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
  ( ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) )
   -> ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( csem ccm1 wcel wa co wceq wi clmgm 3exp imp adantl com12 smgrpass2 3syl
      inss1 sseli adantr simprll simprlr cmagm smgrpismgm com3l syl13anc simplr
      cin w3a simprl simprr 3jca reacomsmgrp1 sylan2 oveq2d simprrl com13 mpan9
      syl eqcomd 3eqtrd ) EHIULZJZAFJZBFJZKZCFJZDFJZKZKZKZABELCDELZELZABVPELZEL
      ZACBDELZELZELZACELVTELZVOEHJZVHVIVPFJZVQVSMVGWDVNVFHEHIUBUCZUDZVGVHVIVMUE
      ZVGVHVIVMUFVGVNWEVGWDEUGJZVNWENWFEUHZVNWIWEVMWIWENZVJVKVLWKWIVKVLWEWIVKVL
      WECDEFGOPUIQRSUAQABVPEFGTUJVOVRWAAEVNVGVIVKVLUMVRWAMVNVIVKVLVHVIVMUKVJVKV
      LUNVJVKVLUOUPBCDEFGUQURUSVOWDVHVKVTFJZWBWCMWGWHVGVJVKVLUTVGWIVNWLVGWDWIWF
      WJVCVJVMWIWLNZVIVMWMNVHVMVIWMVLVIWMNVKWIVIVLWLWIVIVLWLBDEFGOPVARSRQVBWDVH
      VKWLUMKWCWBACVTEFGTVDUJVE $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d k m n x y z G $.  $d k m n x y z M $.  $d k m n x y z N $.
    $d m n x y z A $.  $d m n x y z B $.  $d k n x y z X $.
    fprodadd.1 $e |- X = dom dom G $.
    $( The composite of two finite composites.  (Contributed by FL,
       14-Sep-2010.)  (The proof was shortened by Mario Carneiro,
       30-May-2014.) $)
    fprodadd $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N )
       ( A e. X /\ B e. X ) /\ G e. ( SemiGrp i^i Com1 ) ) ->
       prod_ k e. ( M ... N ) G ( A G B ) =
         ( prod_ k e. ( M ... N ) G A G prod_ k e. ( M ... N ) G B ) ) $=
      ( vm vx cfv wcel wa co wral cv cprd sylan wceq oveq12d vy vz cuz cfz csem
      ccm1 cin w3a cmpt cseq cmagm simp3 elin sylib simpld smgrpismgm syl clmgm
      vn 3expb simprd iscomb smgrpass2 simp1 wf simpl ralimi 3ad2ant2 eqid fmpt
      ffvelrn simpr cvv ovex fvmpt2 mpan2 adantr adantrr eqtr4d ralimiaa hbmpt1
      adantrl ax-17 hbfv hbov fveq2 eqeq12d rcla4 sercaopr fprodser 3eqtr4d wss
      hbeq mpan9 ssid prodeqfv ax-mp 3eqtr3g ) FEUCKLZAGLZBGLZMZCEFUDNZOZDUEUFU
      GLZUHZXCIPZCXCABDNZUIZKIDQZXCXGCXCAUIZKIDQZXCXGCXCBUIZKIDQZDNZXCXHCDQZXCA
      CDQZXCBCDQZDNZXFFDXIEUJKZFDXKEUJKZFDXMEUJKZDNZXJXOXFJUAUBDGUSXKXMXIEFXFDU
      KLZJPZGLZUAPZGLZMZYEYGDNZGLZXFDUELZYDXFYLDUFLZXFXEYLYMMWSXDXEULDUEUFUMUNZ
      UOZDUPUQYDYFYHYKYEYGDGHURUTRXFYMYIYJYGYEDNSZXFYLYMYNVAYMYFYHYPYEYGDGHVBUT
      RXFYLYFYHUBPZGLUHYJYQDNYEYGYQDNDNSYOYEYGYQDGHVCRWSXDXEVDZXFXCGXKVEZUSPZXC
      LZYTXKKZGLXFWTCXCOZYSXDWSUUCXEXBWTCXCWTXAVFVGVHCXCGAXKXKVIZVJUNXCGYTXKVKR
      XFXCGXMVEZUUAYTXMKZGLXFXACXCOZUUEXDWSUUGXEXBXACXCWTXAVLVGVHCXCGBXMXMVIZVJ
      UNXCGYTXMVKRXFCPZXIKZUUIXKKZUUIXMKZDNZSZCXCOZUUAYTXIKZUUBUUFDNZSZXDWSUUOX
      EXBUUNCXCUUIXCLZXBMZUUJXHUUMUUSUUJXHSZXBUUSXHVMLUVAABDVNCXCXHVMXIXIVIZVOV
      PVQUUTUUKAUULBDUUSWTUUKASXACXCAGXKUUDVOVRUUSXAUULBSWTCXCBGXMUUHVOWBTVSVTV
      HUUNUURCYTXCCJJUUPUUQCJYTXICJXCXHWAYEYTLCWCZWDCJUUBUUFDCJYTXKCJXCAWAUVCWD
      YEDLCWCCJYTXMCJXCBWAUVCWDWEWMUUIYTSZUUJUUPUUMUUQUUIYTXIWFUVDUUKUUBUULUUFD
      UUIYTXKWFUUIYTXMWFTWGWHWNWIXFWSXJXTSYRIXIDEFWJUQXFWSXOYCSYRWSXLYAXNYBDIXK
      DEFWJIXMDEFWJTUQWKXCXCWLZXJXPSXCWOZXHXCXCCIXIDUVBWPWQUVEXOXSSUVFUVEXLXQXN
      XRDAXCXCCIXKDUUDWPBXCXCCIXMDUUHWPTWQWR $.
      $( [30-May-2014] $) $( [14-Sep-2010] $)
  $}

  ${
    ablinvop.1 $e |- X = ran G $.
    ablinvop.2 $e |- N = ( inv ` G ) $.
    $( The inverse of the abelian group operation doesn't reverse the
       arguments. cf ~ grpoinvop .  (Contributed by FL, 14-Sep-2010.) $)
    ablinvop $p |- ( ( G e. AbelOp /\ A e. X /\ B e. X ) ->
                   ( N ` ( A G B ) ) = ( ( N ` A ) G ( N ` B ) ) ) $=
      ( cabl wcel w3a cfv cgr wceq ablogrpo grpoinvop syl3an1 grpoinvcl 3adant2
      co simp1 3adant3 ablocom syl3anc eqtrd ) CHIZAEIZBEIZJZABCSDKZBDKZADKZCSZ
      UKUJCSZUECLIZUFUGUIULMCNZABCDEFGOPUHUEUJEIZUKEIZULUMMUEUFUGTUEUNUFUGUPUOU
      NUGUPUFBCDEFGQRPUEUNUFUGUQUOUNUFUQUGACDEFGQUAPUJUKCEFUBUCUD $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d x y z F $.  $d x y z G $.  $d x y z M $.  $d x y z N $.  $d x y z X $.
    $d x y z Z $.
    isppm.1 $e |- X = dom dom G $.
    isppm.2 $e |- Z = ( ZZ>= ` M ) $.
    $( The sequence of partial composites of elements of a magma is a sequence
       of elements of this magma.  (Contributed by FL, 24-Jan-2010.) $)
    isppm $p |- ( ( G e. Magma /\ M e. ZZ /\ F : Z --> X ) ->
      seq M ( G , F ) : Z --> X ) $=
      ( vx vy cmagm wcel cz wf w3a simp2 cv cfv ffvelrn 3ad2antl3 wa clmgm seqf
      co 3expb 3ad2antl1 ) BJKZCLKZEDAMZNHIBDACEGUFUGUHOUHUFHPZEKUIAQDKUGEDUIAR
      SUFUGUIDKZIPZDKZTUIUKBUCDKZUHUFUJULUMUIUKBDFUAUDUEUB $.
      $( [30-May-2014] $) $( [24-Jan-2010] $)

    seqzp2.1 $e |- G e. SemiGrp $.
    seqzp2.2 $e |- F : Z --> X $.
    $( Value of the arbitrary-based recursive sequence builder at a successor
       value when the operation ` G ` is associative.  Compare with ~ seqp1 .
       (Contributed by FL, 24-Jan-2010.) $)
    seqzp2 $p |- ( N e. Z ->
      ( seq M ( G , F ) ` ( N + 1 ) ) =
        ( ( F ` M ) G ( seq ( M + 1 ) ( G , F ) ` ( N + 1 ) ) ) ) $=
      ( vx vy vz wcel c1 caddc co cv adantl cfv wa cmagm smgrpismgm ax-mp clmgm
      csem mp3an1 w3a wceq smgrpass2 mpan cuz eluzp1p1 eleq2s cz eluzel2 elfzuz
      cfz syl6eleqr ffvelrni syl seq1p ) DFNZKLMBEACDOPQZKRZENZLRZENZUAVEVGBQZE
      NZVCBUBNZVFVHVJBUFNZVKIBUCUDVEVGBEGUEUGSVFVHMRZENUHZVIVMBQVEVGVMBQBQUIZVC
      VLVNVOIVEVGVMBEGUJUKSVDCOPQULTNDCULTZFCDUMHUNCUONDVPFCDUPHUNVECVDURQNZVEA
      TENZVCVQVEFNVRVQVEVPFVECVDUQHUSFEVEAJUTVASVB $.
      $( [24-Jan-2010] $)
  $}

  $( A monoid is associative.  (Contributed by FL, 2-Nov-2009.) $)
  mndisass $p |- ( G e. Mnd -> G e. Ass ) $=
    ( cmnd wcel csem cass mndissmgrp smgrpisass syl ) ABCADCAECAFAGH $.
    $( [2-Nov-2009] $)

  ${
    $d G x y z $.  $d X x y z $.
    mndid.1 $e |- X = ran G $.
    $( A monoid has an identity element.  (Contributed by FL, 2-Nov-2009.) $)
    mndid $p |- ( G e. Mnd -> E. x e. X A. y e. X
      ( ( x G y ) = y /\ ( y G x ) = y ) ) $=
      ( vz cmnd wcel cv co wceq wa wral wrex cxp wf w3a ismnd2 simp3 syl6bi
      pm2.43i ) CGHZAIZBIZCJZUDKUDUCCJUDKLBDMADNZUBUBDDODCPZUEFIZCJUCUDUHCJCJKF
      DMBDMADMZUFQUFABFGCDERUGUIUFSTUA $.
      $( [23-Dec-2013] $) $( [2-Nov-2009] $)

    $( A monoid is an internal operation.  (Contributed by FL, 2-Nov-2009.) $)
    mndio $p |- ( G e. Mnd -> G : ( X X. X ) --> X ) $=
      ( vx vy vz cmnd wcel cxp wf cv co wceq wral wrex w3a ismnd2 simp1 syl6bi
      wa pm2.43i ) AGHZBBIBAJZUBUBUCDKZEKZALZFKZALUDUEUGALALMFBNEBNDBNZUFUEMUEU
      DALUEMTEBNDBOZPUCDEFGABCQUCUHUIRSUA $.
      $( [23-Dec-2013] $) $( [2-Nov-2009] $)

    $( A monoid is associative.  (Contributed by FL, 2-Nov-2009.) $)
    mndass $p |- ( G e. Mnd ->
        A. x e. X A. y e. X A. z e. X
          ( ( x G y ) G z ) = ( x G ( y G z ) ) ) $=
      ( cmnd wcel cv co wceq wral cxp wf wa wrex w3a ismnd2 simp2 syl6bi
      pm2.43i ) DGHZAIZBIZDJZCIZDJUCUDUFDJDJKCELBELAELZUBUBEEMEDNZUGUEUDKUDUCDJ
      UDKOBELAEPZQUGABCGDEFRUHUGUISTUA $.
      $( [23-Dec-2013] $) $( [2-Nov-2009] $)

    $d A x y z $.  $d B y z $.  $d C z $.
    $( A monoid is associative.  (Contributed by FL, 12-Dec-2009.) $)
    mndass2 $p |- ( ( G e. Mnd /\ ( A e. X /\ B e. X /\ C e. X ) )
      -> ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( vx vy vz cmnd wcel cv co wceq wral oveq1 oveq1d eqeq12d oveq2 oveq2d
      w3a mndass rcla43v mpan9 ) DJKGLZHLZDMZILZDMZUEUFUHDMZDMZNZIEOHEOGEOAEKBE
      KCEKUAABDMZCDMZABCDMZDMZNZGHIDEFUBULUQAUFDMZUHDMZAUJDMZNUMUHDMZABUHDMZDMZ
      NGHIABCEEEUEANZUIUSUKUTVDUGURUHDUEAUFDPQUEAUJDPRUFBNZUSVAUTVCVEURUMUHDUFB
      ADSQVEUJVBADUFBUHDPTRUHCNZVAUNVCUPUHCUMDSVFVBUOADUHCBDSTRUCUD $.
      $( [12-Dec-2009] $)
  $}

  $( Declare the symbol for the monoid exponentiation. $)
  $c ^md $.

  $( Extend class notation with monoid exponentiation. $)
  clsg $a class ^md $.

  ${
    $d g f x y z $.
    $( Define the exponentiation of an element of a monoid.  Experimental.  I
       define exponentiation on a monoid (and not on a semi-group or a magma )
       because I need an identity element for the basis hypothesis and
       associativity for interesting properties such as the composite of two
       exponentiated elements. ` ZZ ` is used in ~ df-gx here I used ` NN0 `
       because the inverse is not defined in a monoid. $)
    df-expsg $a |- ^md = { <. g , f >. | ( g e. Mnd /\
     f = { <. <. x , y >. , z >. | ( ( x e. ran g /\ y e. NN0 ) /\
       z = if ( y = 0 , ( Id ` g ) ,
         ( seq 1 ( g , ( NN x. { x } ) ) ` y ) ) ) } ) } $.
  $}

  ${
    expmiz.2 $e |- F =
        ( rec ( { <. a , b >. | b = ( a G A ) } , ( Id ` G ) ) |` om ) $.
    ${
      $( Value of a member of a monoid (or any other structure where ` Id ` is
         defined ) raised to the 0th power.  (Contributed by FL,
         12-Dec-2009.) $)
      expmiz $p |- ( F ` (/) ) = ( Id ` G ) $=
        ( c0 cfv cv co wceq copab cgi crdg com cres fveq1i cvv wcel fvex ax-mp
        fr0g eqtri ) GBHGEIDIACJKDELZCMHZNOPZHZUEGBUFFQUERSUGUEKCMTUERUDUBUAUC
        $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d A a b $.  $d F a b $.  $d G a b $.  $d N a b $.
      $( Exponentiation of a monoid.  The value at a successor.  What I am
         calculating is ` ( A ( ^md `` G ) N ) ` .  (Contributed by FL,
         12-Dec-2009.) $)
      expm $p |- ( N e. om ->
      ( F ` suc N ) = ( ( F ` N ) G A ) ) $=
        ( com wcel csuc cv co wceq copab cgi cfv crdg cres fveq1i cvv fvex ovex
        frsuc eqcomi fveq2i oveq1 fvopabg mp2an eqtr3i 3eqtr3g ) DHIDJZFKEKZACL
        ZMEFNZCOPZQHRZPZDUPPZUNPZUKBPZDBPZACLZUODUNUCUTUQUKBUPGSUDVAUNPZUSVBVAU
        RUNDBUPGSUEVATIVBTIVCVBMDBUAVAACUBEFVAUMVBTTULVAACUFUGUHUIUJ $.
        $( [12-Dec-2009] $)
    $}

    expus.1 $e |- G e. Mnd $.
    expus.3 $e |- A e. X $.
    expus.4 $e |- X = ran G $.
    ${
      $d A a b $.  $d F a b y $.  $d F x y $.  $d G a b $.  $d X x y $.
      $( The exponentiation of a member of a monoid belongs to the underlying
         set.  (Contributed by FL, 12-Dec-2009.) $)
      expus $p |- ( x e. om -> ( F ` x ) e. X ) $=
        ( cfv wcel c0 wceq fveq2 eleq1d cmagm cexid ax-mp vy csuc cmnd mndismgm
        cv cin wa mndisexid jca elin sylibr expmiz iorlid com co expm adantr wf
        cxp mndio fovrn mp3an13 adantl eqeltrd ex finds1 ) AUEZCLZEMNCLZEMZUAUE
        ZCLZEMZVKUBZCLZEMZAUAVGNOVHVIEVGNCPQVGVKOVHVLEVGVKCPQVGVNOVHVOEVGVNCPQD
        RSUFMZVJDUCMZVQIVRDRMZDSMZUGVQVRVSVTDUDDUHUIDRSUJUKTVIDEKBCDFGHULUMTVKU
        NMZVMVPWAVMUGVOVLBDUOZEWAVOWBOVMBCDVKFGHUPUQVMWBEMZWAEEUSEDURZVMBEMWCVR
        WDIDEKUTTJVLBEEEDVAVBVCVDVEVF $.
        $( [12-Dec-2009] $)
    $}

  $}

  $( The range of the domain of a magma equals the domain of the domain.
     (Contributed by FL, 17-May-2010.) $)
  mgmrddd $p |- ( G e. Magma -> ran dom G = dom dom G ) $=
    ( cmagm wcel cdm crn wceq cxp eqid ismgm fdm rneq rnxpid syl6eq syl pm2.43i
    wf syl6bi ) ABCZADZEZSDZFZRRUAUAGZUAAPZUBBAUAUAHIUDSUCFZUBUCUAAJUETUCEUASUC
    KUALMNQO $.
    $( [17-May-2010] $)

  ${
    unsgrp.1 $e |- X = ran G $.
    $( The underlying set of a group is a set.  (Contributed by FL,
       17-May-2010.) $)
    unsgrp $p |- ( G e. GrpOp -> X e. _V ) $=
      ( cgr wcel crn cvv rnexg syl5eqel ) ADEBAFGCADHI $.
      $( [17-May-2010] $)
  $}

  ${
    $d G x y z $.
    $( A group operation is a left group action of the group on itself.
       (Contributed by FL, 17-May-2010.) $)
    ltlga $p |- ( G e. GrpOp -> <. G , G >. e. GrpAct ) $=
      ( vx vy vz cgr wcel cop crn cdm cxp wf cv co wceq wral wa eqid syl eqtr4d
      wi cga cgi cfv w3a id grpomnd mndio grporndm cmagm mndismgm mgmrddd xpeq2
      cmnd feq23d biimpcd 3syl pm2.43i eleq2d grpolid grpoass 3exp2 com23 com24
      imp4b ralrimivv jca ex sylbid ralrimiv 3jca isga mpbird ) AEFZAAGUAFVMAHZ
      AIZHZJZVPAKZAUBUCZBLZAMVTNZCLZDLZAMVTAMWBWCVTAMAMNZDVNOCVNOZPZBVPOZUDVMVM
      VRWGVMUEVMVRVMAUMFZVNVNJZVNAKZVMVRTAUFZAVNVNQZUGVMWJVRVMWIVNVQVPAVMVNVPNW
      IVQNVMVNVOIZVPAUHZVMAUIFZVPWMNVMWHWOWKAUJRAUKRZSZVNVPVNULRWQUNUOUPUQVMWFB
      VPVMVTVPFVTVNFZWFVMVPVNVTVMVPWMVNWPWNSURVMWRWFVMWRPZWAWEVTVSAVNWLVSQZUSWS
      WDCDVNVNVMWRWBVNFZWCVNFZWDVMXBXAWRWDVMXAXBWRWDTVMXAXBWRWDWBWCVTAVNWLUTVAV
      BVCVDVEVFVGVHVIVJBCDEVSAAVNVPWLVPQWTVKVL $.
      $( [17-May-2010] $)
  $}


  ${
    $d A f g $.  $d A g x $.  $d P f g $.
    symgfo.1 $e |- A e. _V $.
    symgfo.2 $e |- P = { x | x : A -1-1-onto-> A } $.
    $( The operation of a symetry group is onto.  (Contributed by FL,
       17-May-2010.) $)
    symgfo $p |- ( SymGrp ` A ) : ( P X. P ) -onto-> P $=
      ( vf vg cmagm cexid wcel cgr cdm wceq ax-mp mpbir cv co wa wf1o sylbi cfv
      csymg cin cxp wfo elin wf symgf wb symggrpi fdm dmeq dmxpid expcom eqcoms
      eqtr com12 3syl ismgm wral wrex cid cres f1oi eqid elsymgrn eleqtrri ccom
      cab symgov eleq2i f1of fcoi2 syl adantl eqtrd mpan fcoi1 adantr mpan2 jca
      wi rgen oveq1 eqeq1d oveq2 anbi12d ralbidv rcla4ev isexid mpbir2an opidon
      mp2an ) BUBUAZHIUCJZCCUDZCWNUEWOWNHJZWNIJZWNHIUFWQWPCWNUGZABCDEUHZWNKJZWQ
      WSUIBDUJZKWNCWSCWNLZLZMZWTWSXCWPMXDWPLZMZXEWPCWNUKXCWPULXFCMXGXEWBZCUMXHC
      XFXGCXFMZXEXIXEWBXFXDXIXFXDMXECXFXDUPUNUOUQUONURNZUSNOWRFPZGPZWNQZXLMZXLX
      KWNQZXLMZRZGCUTZFCVAZVBBVCZCJZXTXLWNQZXLMZXLXTWNQZXLMZRZGCUTZXSXTBBAPSAVI
      ZCXTYHJBBXTSBVDABYHXTDYHVEZVFOEVGZYFGCXLCJZYCYEYAYKYCYJYAYKRYBXTXLVHZXLAB
      CXTXLDEVJYKYLXLMZYAYKXLYHJZYMCYHXLEVKZYNBBXLSZYMABYHXLDYIVFZYPBBXLUGZYMBB
      XLVLZBBXLVMVNTTVOVPVQYKYAYEYJYKYARYDXLXTVHZXLABCXLXTDEVJYKYTXLMZYAYKYNUUA
      YOYNYPUUAYQYPYRUUAYSBBXLVRVNTTVSVPVTWAWCXRYGFXTCXKXTMZXQYFGCUUBXNYCXPYEUU
      BXMYBXLXKXTXLWNWDWEUUBXOYDXLXKXTXLWNWFWEWGWHWIWMXAWRXSUIXBFGKWNCXJWJNOWKW
      NCXJWLN $.
      $( [23-Dec-2013] $) $( [17-May-2010] $)
  $}

  ${
    gaplc.1 $e |- X = ran G $.
    gaplc.2 $e |- Y = ran M $.
    $( The action of a particular group element is left-cancelable.
       (Contributed by FL, 17-May-2010.) $)
    gaplc $p |- ( ( M e. N /\ <. G , M >. e. GrpAct /\
                  ( C e. X /\ A e. Y /\ B e. Y ) )
      -> ( ( C M A ) = ( C M B ) -> A = B ) ) $=
      ( wcel w3a cvv cxp wf1o wceq wi wa cfv 3ad2ant3 cop cga crn c2nd csn cres
      ccnv ccom co eleq2i biimpi 3ad2ant1 eqid gapm syl3an3 ccur1 wne wfn gagrp
      c0 cgr unsgrp rnexg syl5eqel adantr anim12i mpancom 3adant3 ne0i 3ad2ant2
      wf gaf xpeq1 eqcomd ax-mp xpeq2 eqtri ffn fneq2 biimpd mpsyl syl jca 3jca
      3simpa 3simpb valvalcurfn eqeqan12d simp31 valcurfn1 f1oeq1 f1oeq2 f1oeq3
      eqcom syl121anc bitrd f1fveq expcom 3adant1 syl5com syl6bi eqcoms syl6bir
      wf1 f1of1 com23 mpcom imp syl9r mpd mpdan ) EFKZDEUAUBKZCGKZAHKZBHKZLZLZE
      UCZXSEUDCUEMNUFUGUHZOZCAEUIZCBEUIZPZABPZQZXQXLXMCDUCZKZYAXNXOYHXPXNYHGYGC
      IUJUKULCFXTDEYGXSYGUMZXSUMZXTUMZUNUOXRYARZACEUPSSZSZYBPZBYMSZYCPZRZYFYLGM
      KZHMKZRZHUTUQZEGHNZURZRZXNXORZLZUUAUUEXNXPRZLZRZYRXRUUJYAXRUUGUUIXRUUAUUE
      UUFXLXMUUAXQDVAKZXLXMRZUUAFDEUSUUKYSUULYTDGIVBXLYTXMXLHXSMJEFVCVDVEVFVGVH
      ZXRUUBUUDXQXLUUBXMXOXNUUBXPHAVIVJTZXLXMUUDXQUULYGXSNZXSEVKZUUDFDEYGXSYIYJ
      VLUUOUUCPZUUPEUUOURZUUDUUOGXSNZUUCGYGPZUUOUUSPIUUTUUSUUOGYGXSVMVNVOHXSPZU
      USUUCPJUVAUUCUUSHXSGVPVNVOVQUUOXSEVRUUQUURUUDUUOUUCEVSVTWAWBVHZWCZXQXLUUF
      XMXNXOXPWETWDXRUUAUUEUUHUUMUVCXQXLUUHXMXNXOXPWFTWDWCVEUUGYOUUIYQGHMMAECWG
      GHMMBECWGVFWBYRYDYNYPPZYLYEYRYDUVDYOYQYBYNYCYPYOYBYNPYNYBWNUKYQYCYPPYPYCW
      NUKWHVTXRYAUVDYEQZYMXTPZXRYAUVEQXRUUAUUBUUDXNUVFUUMUUNUVBXLXMXNXOXPWIGHMM
      EXTCYKWJWOUVFYAXRUVEUVFYAXSXSYMOZXRUVEQZXSXSYMXTWKUVAUVGUVHQZJUVIXSHXSHPZ
      UVGHHYMOZUVHUVJUVGHXSYMOUVKXSHXSYMWLXSHHYMWMWPUVKHHYMXDZXRUVEHHYMXEXQXLUV
      LUVEQZXMXOXPUVMXNUVLXOXPRZUVEUVLUVNRUVDYEHHABYMWQVTWRWSTWTXAXBVOXCXFXGXHX
      IXJXK $.
      $( [17-May-2010] $)
  $}

  ${
    gapm2.1 $e |- X = ran G $.
    gapm2.2 $e |- Y = ran M $.
    ${
      $( The action of a particular group element is a permutation of the base
         set. ~ gapm expressed with the currying operator.  (Contributed by FL,
         17-May-2010.) $)
      gapm2 $p |-
        ( ( M e. B /\ <. G , M >. e. GrpAct /\ ( A e. X /\ Y =/= (/) ) ) ->
        ( ( cur1 ` M ) ` A ) : Y -1-1-onto-> Y ) $=
        ( wcel cop cga c0 wa cfv cvv cxp wceq wf1o syl 3adant3 wne w3a c2nd csn
        ccur1 cres ccnv wfn cgr gagrp unsgrp crn rnexg syl5eqel 3ad2ant1 simp3r
        ccom wf gaf ffn simp3l eqid valcurfn1 syl221anc gapm 3adant3r wi f1oeq1
        biimpd eqcoms sylc ) DBIZCDJKIZAEIZFLUAZMZUBZADUENNZDUCAUDOPUFUGUQZQZFF
        VSRZFFVRRZVQEOIZFOIZVODEFPZUHZVNVTVLVMWCVPVLVMMZCUIIWCBCDUJCEGUKSTVLVMW
        DVPVLFDULOHDBUMUNUOVLVMVNVOUPVLVMWFVPWGWEFDURWFBCDEFGHUSWEFDUTSTVLVMVNV
        OVAEFOODVSAVSVBZVCVDVLVMVNWAVOABVSCDEFGHWHVEVFWAWBVGVSVRVSVRQWAWBFFVSVR
        VHVIVJVK $.
        $( [17-May-2010] $)
    $}

    ${
      $( The range of the action of a particular group element equals the range
         of the action.  (Contributed by FL, 17-May-2010.) $)
      rngapm $p |- (
        ( M e. B /\ <. G , M >. e. GrpAct /\ ( A e. X /\ Y =/= (/) ) )
        -> ran ( ( cur1 ` M ) ` A ) = ran M ) $=
        ( wcel cop cga c0 wne wa w3a ccur1 cfv wf1o crn wceq gapm2 f1ofo eqeq2i
        wfo forn biimpi 3syl syl ) DBICDJKIAEIFLMNOFFADPQQZRZUISZDSZTZABCDEFGHU
        AUJFFUIUDUKFTZUMFFUIUBFFUIUEUNUMFULUKHUCUFUGUH $.
        $( [17-May-2010] $)
    $}
  $}

  ${
    $( The proof has been designed so that B and x needn't be distinct. $)
    $d A x y $.  $d B y $.  $d C x y $.  $d F t $.  $d G t $.  $d H t x $.
    $d H x y $.
    fnopabco2b.2 $e |- F = { <. x , y >. | ( x e. A /\ y = B ) } $.
    fnopabco2b. $e |- G = { <. x , y >. | ( x e. A /\ y = ( H ` B ) ) } $.
    $( Composition of a function with a function abstraction.  Adapted from
       ~ fnopabco .  (Contributed by FL, 17-May-2010.) $)
    fnopabco2b $p |- ( ( A. x e. A B e. C /\ H Fn C )
        -> G = ( H o. F ) ) $=
      ( vt wcel wral wfn wa wceq cv cfv wi cvv ccom hbra1 ax-17 hban ra4 eqcomi
      copab fveq1i fvopab2 syl5eqr ancoms 3adant3 fveq2d 3adant2r wfun 3ad2ant3
      w3a fnfun elex ralimi fnopab2g sylib adantl 3ad2ant2 simp2l fvco2 syl3anc
      simpl fvex jctir syl syl5eq 3eqtr4rd syl6com imp pm2.43i ex com3l ralrimi
      3exp wb fnopab2 crn wss simpr adantr wf fopab2 biimpi frn hbopab1 hbxfreq
      fnco hbco eqfnfv2f sylancr mpbird ) DELZACMZHENZOZGHFUAZPZAQZGRZXDXBRZPZA
      CMZXAXGACWSWTAWRACUBWTAUCUDWSWTXDCLZXGSXIWSWTXGXIWSWTXGSZXIWSOZXJXIWSXKXJ
      SZWSXIWRXLWRACUEWRXKWTXGWRXKWTUQZXDFRZHRZDHRZXFXEWRXIWTXOXPPWSWRXIWTUQXND
      HWRXIXNDPZWTXIWRXQXIWROXNXDXIBQZDPOZABUGZRDXDXTFFXTIUFUHABCDEUIUJUKULUMUN
      XMHUOZFCNZXIXFXOPWTWRYAXKEHURUPXKWRYBWTWSYBXIWSDTLZACMYBWRYCACDEUSUTABCDF
      IVAVBZVCVDWRXIWSWTVECXDHFVFVGXMXEXDXIXRXPPOZABUGZRZXPXDGYFJUHXMXIXPTLZOZY
      GXPPXKWRYIWTXKXIYHXIWSVHDHVIZVJVDABCXPTUIVKVLVMVTVNVOVPVQVRVOVSXAGCNXBCNZ
      XCXHWAABCXPGYJJWBXAWTYBFWCEWDZYKWSWTWEWSYBWTYDWFXACEFWGZYLWSYMWTWSYMABCED
      FIWHWIWFCEFWJVKECHFWMVGAKCGXBAKGYFJYEABKWKWLAKHFKQHLAUCAKFXTIXSABKWKWLWNW
      OWPWQ $.
      $( [17-May-2010] $)
  $}

  ${
    $d G a b m n x y $.  $d G a f m n x y $.  $d M a b m n x y $.
    $d M a f m n x y $.  $d M a b x y z $.
    curgrpact.1 $e |- M e. A $.
    curgrpact.2 $e |- ran M =/= (/) $.
    $( The currying of a group action is a group homomorphism between the group
       ` G ` and the symetry group ` ( SymGrp `` ran M ) ` .  (Contributed by
       FL, 17-May-2010.) $)
    curgrpact $p |- ( <. G , M >. e. GrpAct ->
      ( cur1 ` M ) e. ( G GrpHom ( SymGrp ` ran M ) ) ) $=
      ( vf vx vy va vm wcel cfv co wi wceq wral wa cvv adantr ax-17 vz vb ccur1
      vn cop cga crn csymg cghom cgr cxp wf cgi cv w3a eqid isga2 wf1o cab wfun
      wfn cdm wne rnexg 3ad2ant1 ax-mp a1i ffn 3ad2ant2 jctil jca31 domrancur1c
      c0 ffun 3syl syl21anc fdm syl df-fn sylanbrc jctir simpr 3jca valcurfn wb
      bicomd biimpi rngapm syl3anc hbra1 hban hbral ad2antrr simpllr simplr jca
      hb3an valvalcurfn anim12i syl33anc eqeq12 elexi gaplc syl113anc syl9r mpd
      biimpd ralrimi dff1o6 sylibr fvex f1oeq1 elab ralrimiv ffnfv copab simprl
      valcurfn2 simprr grpocl 3expib imp oveq12 3adant3 adantl ccom simpl gapm2
      ex elabg eqeltrrd impcom ovex eqcomd oveq2 sylancl eqeq12d ralbidv 3eqtrd
      3ad2ant3 adantlr adantll symgov fovrn 3exp com12 rgen fnopab2g fnopabco2b
      ancoms sylib syl2anc fvopab4g oveq2d anbi12d cbvralv ra4 ra42 com23 sylbi
      id syl6 imp31 eqtrd eqeq2d pm5.32da opabbidv eqcom mpdan expdimp symggrpi
      simp1 rnex wfo symgfo forn elghom mpbird syl6bi ) CAKZBCUEUFKZCUCLZBCUGZU
      HLZUIMKZNDUVTUWABUJKZBUGZUWCUKZUWCCULZBUMLZFUNZCMZUWKOZGUNZHUNZBMZUWKCMZU
      WNUWOUWKCMZCMZOZHUWGPZGUWGPZQZFUWCPZUOZUWEFGHAUWJBCUWGUWCUWGUPZUWCUPZUWJU
      PUQZUXEUWEUWGUWCUWCUWKURZFUSZUWBULZUWNUWBLZUWOUWBLZUWDMZUWPUWBLZOZHUWGPZG
      UWGPZQZUXEUXKUXRUXEUWBUWGVAZIUNZUWBLZUXJKZIUWGPUXKUXEUWBUTZUWBVBUWGOZUXTU
      XEUWGRKZUWCRKZQZUWCVMVCZCUWHVAZQZQUWGUWCUWCUAUNZULUAUSZUWBULZUYDUXEUYFUYG
      UYKUWFUWIUYFUXDBUJVDVEZUYGUXEUVTUYGDCAVDVFZVGZUXEUYJUYIUWIUWFUYJUXDUWHUWC
      CVHVIZEVJZVKUWGUWCRRUACVLZUWGUYMUWBVNVOUXEUYNUYEUXEUYFUYGUYKUYNUYOUYQUYSU
      YTVPUWGUYMUWBVQVRUWBUWGVSVTUXEUYCIUWGUXEUYAUWGKZUYCUXEVUAQZUWCUWCUYBURZUY
      CVUBUYBUWCVAZUYBUGUWCOZUWNUYBLZUWOUYBLZOZUWNUWOOZNZHUWCPZGUWCPZUOVUCVUBVU
      DVUEVULVUBUYHUYKVUAUOUWCUWCUYBULVUDVUBUYHUYKVUAUXEUYHVUAUXEUYFUYGUYOUYPWA
      ZSZVUBUYJUYIUXEUYJVUAUYRSEVJZUXEVUAWBZWCUWGUWCRRCUYAWDUWCUWCUYBVHVOVUBUVT
      UWAVUAUYIQVUEUVTVUBDVGUXEUWAVUAUXEUWAUVTUXEUWAWEDUVTUWAUXEUXHWFVFWGZSZVUB
      VUAUYIVUPEWAUYAABCUWGUWCUXFUXGWHWIVUBVUKGUWCUXEVUAGUWFUWIUXDGUWFGTUWIGTUX
      CGFUWCUWKUWCKZGTUWMUXBGUWMGTUXAGUWGWJWKWLWQZVUAGTWKVUBUWNUWCKZVUKVUBVVAQZ
      VUJHUWCVUBVVAHUXEVUAHUWFUWIUXDHUWFHTUWIHTUXCHFUWCVUSHTUWMUXBHUWMHTUXAHGUW
      GUWNUWGKZHTZUWTHUWGWJWLWKWLWQZVUAHTWKVVAHTWKVVBUWOUWCKZVUJVVBVVFQZVUFUYAU
      WNCMZOZVUGUYAUWOCMZOZQZVUJVVGUYHUYKVUAVVAQZUYHUYKVUAVVFQZVVLVUBUYHVVAVVFV
      UNWMZVUBUYKVVAVVFVUOWMZVVGVUAVVAUXEVUAVVAVVFWNZVUBVVAVVFWOZWPVVOVVPVVGVUA
      VVFVVQVVBVVFWBZWPUYHUYKVVMUOVVIUYHUYKVVNUOVVKUWGUWCRRUWNCUYAWRUWGUWCRRUWO
      CUYAWRWSWTVVLVUHVVHVVJOZVVGVUIVVLVUHVVTVUFVVHVUGVVJXAXGVVGCRKZUWAVUAVVAVV
      FVVTVUINVWAVVGCADXBZVGVUBUWAVVAVVFVURWMVVQVVRVVSUWNUWOUYABCRUWGUWCUXFUXGX
      CXDXEXFYIXHYIXHWCGHUWCUWCUYBXIXJUXIVUCFUYBUYAUWBXKUWCUWCUWKUYBXLXMXJYIXNI
      UWGUXJUWBXOVTUXEUXQGUWGVUTUXEVVCUXQUXEVVCQUXPHUWGUXEVVCHVVEVVDWKUXEVVCUWO
      UWGKZUXPUXEVVCVWCQZUXPUXEVWDQZUXLUYAUWCKZUBUNUWNUYACMZOQIUBXPZOZUXMJUNZUW
      CKZUDUNZUWOVWJCMZOQJUDXPZOZUXOVWKVWLUWPVWJCMZOZQZJUDXPZOZUOZUXPVWEVWIVWOV
      WTVWEUYHUYKVVCVWIUXEUYHVWDVUMSZUXEUYKVWDUYSSZUXEVVCVWCXQZIUBUWGUWCRRCUWNX
      RWIVWEUYHUYKVWCVWOVXBVXCUXEVVCVWCXSZJUDUWGUWCRRCUWOXRWIVWEUYHUYKUWPUWGKZV
      WTVXBVXCUXEVWDVXFUWFUWIVWDVXFNUXDUWFVVCVWCVXFUWNUWOBUWGUXFXTYAVEYBJUDUWGU
      WCRRCUWPXRWIWCVWEVXAQZUXNVWHVWNUWDMZVWSUXOVXAUXNVXHOZVWEVWIVWOVXIVWTUXLVW
      HUXMVWNUWDYCYDYEVXGVXHVWHVWNYFZVWKVWLVWMVWHLZOZQZJUDXPZVWSVXGVWHUWCUWCUYL
      URZUAUSZKZVWNVXPKZQZVXHVXJOVXAVWEVXSVWIVWOVWEVXSNVWTVWIVWOQZVWEVXSVXTVWEQ
      VXQVXRVWIVWEVXQVWOVWIVWEQUXLVWHVXPVWIVWEYGVWEUXLVXPKZVWIVWEUWCUWCUXLURZVY
      AVWEUVTUWAVVCUYIQVYBUVTVWEDVGZUXEUWAVWDVUQSZVWEVVCUYIVXDEWAUWNABCUWGUWCUX
      FUXGYHWIUXLRKVYAVYBWEUWNUWBXKVXOVYBUAUXLRUWCUWCUYLUXLXLYJVFXJYEYKUUAVWOVW
      EVXRVWIVWOVWEQUXMVWNVXPVWOVWEYGVWEUXMVXPKZVWOVWEUWCUWCUXMURZVYEVWEUVTUWAV
      WCUYIQVYFVYCVYDVWEVWCUYIVXEEWAUWOABCUWGUWCUXFUXGYHWIUXMRKVYEVYFWEUWOUWBXK
      VXOVYFUAUXMRUWCUWCUYLUXMXLYJVFXJYEYKUUBWPYIYDYLUAUWCVXPVWHVWNUYPVXPUPUUCV
      RVXGVWMUWCKZJUWCPZVWHUWCVAZVXJVXNOVWEVYHVXAVWDUXEVYHVWDUXEQVYGJUWCVWDUXEV
      WKVYGNZVWCUXEVYJNVVCUXEVWCVYJUWIUWFVWCVYJNUXDUWIVWCVWKVYGUWOVWJUWCUWGUWCC
      UUDUUEVIUUFYEZYBXNUUJSVWEVYIVXAVWEVWGRKZIUWCPZVYIVYMVWEVYLIUWCVYLVWFUWNUY
      ACYMVGUUGVGIUBUWCVWGVWHVWHUPZUUHUUKSVYHVYIQVXNVXJJUDUWCVWMUWCVWNVXNVWHVWN
      UPVXNUPUUIYNUULVWEVXNVWSOVXAVWEVXMVWRJUDVWEVWKVXLVWQVWEVWKQZVXKVWPVWLVYOV
      XKUWNVWMCMZVWPVYOVYGVYPRKVXKVYPOVWEVWKVYGVWDUXEVYJVYKYLYBUWNVWMCYMIUBVWMV
      WGVYPUWCRVWHUYAVWMUWNCYOVYNUUMYPUXEVWDVWKVYPVWPOZUXDUWFVWDVWKVYQNNZUWIUXD
      UWJVWJCMZVWJOZVWPVYPOZHUWGPZGUWGPZQZJUWCPZVYRUXCWUDFJUWCUWKVWJOZUWMVYTUXB
      WUCWUFUWLVYSUWKVWJUWKVWJUWJCYOWUFUVAYQWUFUXAWUBGUWGWUFUWTWUAHUWGWUFUWQVWP
      UWSVYPUWKVWJUWPCYOWUFUWRVWMUWNCUWKVWJUWOCYOUUNYQYRYRUUOUUPWUEVWKVWDVYQWUE
      VWKWUDVWDVYQNZWUDJUWCUUQWUCWUGVYTWUCVWDVYQWUCVWDQVWPVYPWUCVWDWUAWUAGHUWGU
      WGUURYBYNYIYEUVBUUSUUTYTUVCUVDUVEUVFUVGSYSVXAVWSUXOOZVWEVWTVWIWUHVWOVWTWU
      HUXOVWSUVHWGYTYEYSUVIYIUVJXHYIXHWPUXEUWFUWDUJKUWEUXSWEUWFUWIUXDUVLUWCCVWB
      UVMZUVKGHUWBBUWDUXJUWGUXFUXJUXJUKZUXJUWDUVNZUXJUWDUGZOFUWCUXJWUIUXJUPUVOW
      UKWULUXJWUJUXJUWDUVPYNVFUVQYPUVRUVSVF $.
      $( [17-May-2010] $)
  $}

  ${
    grpdivfo.1 $e |- X = ran G $.
    grpdivfo.2 $e |- D = ( /g ` G ) $.
    ${
      grpdivfo.3 $e |- U = ( Id ` G ) $.
      $( "Division" by the neutral element of a group.  (Contributed by FL,
         21-Jun-2010.) $)
      grpodivone $p |- ( ( G e. GrpOp /\ A e. X ) -> ( A D U ) = A ) $=
        ( cgr wcel co wceq grpoidcl w3a cgn cfv wi eqtr ex syl grpodivval oveq2
        eqid grpoinvid wa grporid syl5com 3adant3 com12 syl6 com3l 3ad2ant1 mpd
        pm2.43i 3exp mpid imp ) DIJZAEJZACBKZALZURUSCEJZVACDEFHMURUSVBVAURUSVBN
        ZUTACDOPZPZDKZLZVAACBDVDEFVDUCZGUAVCVGVAQZURUSVCVIQZVBURVECLZVJCDVDHVHU
        DVKVFACDKZLZVJVECADUBVGVMVCVAVGVMUTVLLZVCVAQVGVMVNUTVFVLRSVCVNVAURUSVNV
        AQVBURUSUEVLALZVNVAACDEFHUFVNVOVAUTVLARSUGUHUIUJUKTTULUNUMUOUPUQ $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d D x y z $.  $d G x y z $.  $d X x y z $.
      $( Todo: maybe suppress grpdivf. $)
      $( A "division" maps onto the group's underlying set.  (Contributed by
         FL, 21-Jun-2010.) $)
      grpodivfo $p |- ( G e. GrpOp -> D : ( X X. X ) -onto-> X ) $=
        ( vx vy vz wcel wceq wa cv co wrex syl wb wi eqcoms com12 3exp grpodivf
        cgr cxp wfo wf crn cab wfn ffn fnrnov fovrn eleq1 biimpd com3l rexlimiv
        wal w3a com34 cgi cfv grpoidcl grpodivone rcla4eov adantl com3r pm2.43i
        eqid ex impbid alrimiv abeq1 a1i mpbird eqtrd jca dffo2 ) BUBIZCCUCZCAU
        DZVRCAUEZAUFZCJZKZVQVTWBABCDEUAZVQWAFLZGLZHLZAMZJZHCNZGCNZFUGZCVQAVRUHZ
        WAWLJVQVTWMWDVRCAUIOGHFCCAUJOVQWLCJZWKWECIZPZFUPZVQWPFVQWKWOWKVQWOWJVQW
        OQZGCWJWFCIZWRWIWSWRQHCWSWGCIZWIWRWSWTVQWIWOVQWSWTWIWOQZVQVTWSWTXAQQWDV
        TWSWTXAVTWSWTUQWHCIZXAWFWGCCCAUKWIXBWOXBWOQWHWEWHWEJXBWOWHWECULUMRSOTOU
        NURUNUOSUOSVQWOWKQZVQBUSUTZCIZVQXCQXDBCDXDVGZVAVQWOXEWKVQWOXEWKQZVQWOKZ
        XGXHWEXDAMZWEJXHXGQZWEAXDBCDEXFVBXJWEXIXHXEWEXIJZWKWOXEXKWKQQVQWOXEXKWK
        GHCCWEXDWEAVCTVDVEROVFVHVEOVFVIVJWNWQPVQWKFCVKVLVMVNVOVSWCPVQVRCAVPVLVM
        $.
        $( [21-Jun-2010] $)
    $}
  $}


  ${
    grpdrcan.1 $e |- X = ran G $.
    grpdrcan.2 $e |- D = ( /g ` G ) $.
    $( Right cancellation law for group "subtraction" (or "division").
       (Contributed by FL, 14-Sep-2010.) $)
    grpodrcan $p |- ( ( G e. GrpOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( A D C ) = ( B D C ) <-> A = B ) ) $=
      ( co cgn cfv wceq cgr wcel w3a wb grpodivval bibi1d syl5ibr mpcom wa eqid
      3adant3r2 3adant3r1 simpr1 simpr2 grpoinvcl 3ad2antr3 3jca grporcan eqeq2
      syldan eqeq1 ) ACDIZACEJKZKZEIZLZEMNZAFNZBFNZCFNZOZUAZUNBCDIZLZABLZPZUSUT
      VBURVAACDEUOFGUOUBZHQUCVDVHURUQVELZVGPZVEBUPEIZLZVDVKUSVAVBVMUTBCDEUOFGVI
      HQUDVDVKVMUQVLLZVGPZUSVCUTVAUPFNZOVOVDUTVAVPUSUTVAVBUEUSUTVAVBUFUSUTVBVPV
      ACEUOFGVIUGUHUIABUPEFGUJULVMVJVNVGVEVLUQUKRSTURVFVJVGUNUQVEUMRST $.
      $( [14-Sep-2010] $)


    $( Left cancellation law for group "subtraction" (or "division").
       (Contributed by FL, 14-Sep-2010.) $)
    grpodlcan $p |- ( ( G e. GrpOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( C D A ) = ( C D B ) <-> A = B ) ) $=
      ( co cfv wceq wa wcel w3a wb wi grpodivval 3exp com13 imp cgn cgr 3adant2
      eqid impcom 3adant1 jca simpl grpoinvcl 3ad2antr1 3ad2antr2 simpr3 adantl
      3jca grpolcan syl wf1 wf1o grpoinvf f1of1 ad2antrl 3simpa ad2antll f1fveq
      syl2anc bitrd ex eqeq12 bibi1d sylibrd mpcom ) CADIZCAEUAJZJZEIZKZCBDIZCB
      VMJZEIZKZLZEUBMZAFMZBFMZCFMZNZLZVLVQKZABKZOZWGVPVTWFWBVPWCWEWBVPPZWDWCWEW
      KWBWEWCVPWBWEWCVPCADEVMFGVMUDZHQRSTUCUEWFWBVTWDWEWBVTPZWCWDWEWMWBWEWDVTWB
      WEWDVTCBDEVMFGWLHQRSTUFUEUGWAWGVOVSKZWIOZWJWAWGWOWAWGLZWNVNVRKZWIWPWBVNFM
      ZVRFMZWENZLZWNWQOWGXAWAWGWBWTWBWFUHWGWRWSWEWBWDWCWRWEAEVMFGWLUIUJWBWCWDWS
      WEBEVMFGWLUIUKWBWCWDWEULUNUGUMVNVRCEFGUOUPWPFFVMUQZWCWDLZWQWIOWBXBWAWFWBF
      FVMURXBEVMFGWLUSFFVMUTUPVAWFXCWAWBWCWDWEVBVCFFABVMVDVEVFVGWAWHWNWIVLVOVQV
      SVHVIVJVK $.
      $( [14-Sep-2010] $)
  $}


  ${
    grpdivzer.1 $e |- X = ran G $.
    grpdivzer.2 $e |- U = ( Id ` G ) $.
    grpdivzer.3 $e |- D = ( /g ` G ) $.
    $( Condition for a "subtraction" (or "division") value to be equal to the
       identity element.  (Contributed by FL, 14-Sep-2010.) $)
    grpodivzer $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
                   ( ( A D B ) = U <-> A = B ) ) $=
      ( cgr wcel w3a co wceq cgn cfv eqid grpodivval eqeq1d wb 3adant2 syld3an3
      grpoinvcl grpoinvid1 wf1 wa wf1o grpoinvf f1of1 syl 3simpc f1fveq syl2anc
      3ad2ant1 3bitr2d ) EJKZAFKZBFKZLZABCMZDNABEOPZPZEMZDNZAVAPVBNZABNZUSUTVCD
      ABCEVAFGVAQZIRSUPUQURVBFKZVEVDTUPURVHUQBEVAFGVGUCUAAVBDEVAFGHVGUDUBUSFFVA
      UEZUQURUFVEVFTUPUQVIURUPFFVAUGVIEVAFGVGUHFFVAUIUJUNUPUQURUKFFABVAULUMUO
      $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d m x y A $.  $d k m x y M1 $.  $d k m x y M2 $.  $d k m x y N $.
    $d x y G $.  $d k x y X $.
    fprodneg.1 $e |- X = ran G $.
    fprodneg.2 $e |- N = ( inv ` G ) $.
    $( The inverse of a finite composite in the case of an abelian group.
       (Contributed by FL, 14-Sep-2010.)  (The proof was shortened by Mario
       Carneiro, 30-May-2014.) $)
    fprodneg $p |- ( ( M2 e. ( ZZ>= ` M1 ) /\ A. k e. ( M1 ... M2 )
       A e. X /\ G e. AbelOp ) ->
       ( N ` prod_ k e. ( M1 ... M2 ) G A ) =
         prod_ k e. ( M1 ... M2 ) G ( N ` A ) ) $=
      ( vm vy cfv wcel co cv cprd wa sylan wceq fveq2d vx cuz cfz wral cabl w3a
      cmpt cseq cgr simp3 ablogrpo syl grpocl 3expb wf simp2 eqid sylib ffvelrn
      fmpt simp1 ablocom adantr simprr simprl grpoinvop syl3anc fvmpt2 cvv fvex
      eqtrd mpan2 eqtr4d ralimiaa 3ad2ant2 ax-17 hbmpt1 hbfv hbeq fveq2 eqeq12d
      rcla4 mpan9 seqhomo fprodser 3ad2ant1 3eqtr4d ssid prodeqfv ax-mp 3eqtr3g
      wss fveq2i ) GFUBLMZAEMZBFGUCNZUDZCUEMZUFZWPJOZBWPAUGZLJCPZDLZWPWTBWPADLZ
      UGZLJCPZWPABCPZDLWPXDBCPZWSGCXAFUHLZDLGCXEFUHLZXCXFWSUAKCCEXAXEDFGWSCUIMZ
      UAOZEMZKOZEMZQZXLXNCNZEMZWSWRXKWNWQWRUJZCUKULZXKXMXOXRXLXNCEHUMUNRWSWPEXA
      UOZXLWPMZXLXALZEMWSWQYAWNWQWRUPBWPEAXAXAUQZUTURWPEXLXAUSRWNWQWRVAWSXPQZXQ
      DLXNXLCNZDLZXLDLXNDLCNZYEXQYFDWSWRXPXQYFSZXSWRXMXOYIXLXNCEHVBUNRTYEXKXOXM
      YGYHSWSXKXPXTVCWSXMXOVDWSXMXOVEXNXLCDEHIVFVGVKWSBOZXALZDLZYJXELZSZBWPUDZY
      BYCDLZXLXELZSZWQWNYOWRWOYNBWPYJWPMZWOQZYLXDYMYTYKADBWPAEXAYDVHTYSYMXDSZWO
      YSXDVIMUUAADVJBWPXDVIXEXEUQZVHVLVCVMVNVOYNYRBXLWPBKKYPYQBKYCDXNDMBVPBKXLX
      ABKWPAVQXNXLMBVPZVRVRBKXLXEBKWPXDVQUUCVRVSYJXLSZYLYPYMYQUUDYKYCDYJXLXAVTT
      YJXLXEVTWAWBWCWDWSXBXIDWNWQXBXISWRJXACFGWEWFTWNWQXFXJSWRJXECFGWEWFWGXBXGD
      WPWPWLZXBXGSWPWHZAWPWPBJXACYDWIWJWMUUEXFXHSUUFXDWPWPBJXECUUBWIWJWK $.
      $( [30-May-2014] $) $( [14-Sep-2010] $)
  $}

  ${
    $d n u v w x y z A $.  $d k n t u v w x y z M $.  $d k n t u v w x y z N $.
    $d n t v w x y z B $.  $d k n v w x y z D $.  $d k n w x y z X $.
    $d n w x y z G $.
    fprodsub.1 $e |- X = ran G $.
    fprodsub.2 $e |- D = ( /g ` G ) $.
    $( The "difference" (or "quotient") of two finite composites.  (Contributed
       by FL, 14-Sep-2010.) $)
    fprodsub $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N )
       ( A e. X /\ B e. X ) /\ G e. AbelOp ) ->
       prod_ k e. ( M ... N ) G ( A D B ) =
         ( prod_ k e. ( M ... N ) G A D prod_ k e. ( M ... N ) G B ) ) $=
      ( vv vx cfv wcel wa co cv cprd sylan wceq vu vt vy vz vw vn cuz wral cabl
      cfz w3a cmpt cseq cgr simp3 ablogrpo grpocl 3expb grpodivcl simpl simprll
      syl simprlr simprrl ablmuldiv oveq1d adantr syl3anc simprrr grpomuldivass
      syl13anc abldivdiv4 3eqtr3rd simp1 wf ralimi 3ad2ant2 sylib ffvelrn simpr
      eqid fmpt cvv ovex fvmpt2 mpan2 anim12dan oveq12 eqtr4d ex ralimia hbmpt1
      ax-17 hbfv hbov hbeq fveq2 oveq12d eqeq12d rcla4 impcom fprodser 3ad2ant1
      sercaopr2 3eqtr4d wss ssid prodeqfv ax-mp 3eqtr3g ) GFUGMNZAHNZBHNZOZDFGU
      JPZUHZEUINZUKZXOKQDXOABCPZULZMKERZXOUAQDXOAULZMUAERZXOUBQDXOBULZMUBERZCPZ
      XOXSDERZXOADERZXOBDERZCPZXRGEXTFUMMZGEYBFUMMZGEYDFUMMZCPZYAYFXRLUCUDUEECH
      UFYBYDXTFGXREUNNZLQZHNZUCQZHNZOZYPYREPZHNZXRXQYOXKXPXQUOZEUPZVBZYOYQYSUUB
      YPYREHIUQZURSXRYOYTYPYRCPHNZUUEYOYQYSUUGYPYRCEHIJUSURSXRXQYTUDQZHNZUEQZHN
      ZOZOZYPUUHCPZYRUUJCPEPZUUAUUHUUJEPCPZTUUCXQUUMOZUUAUUHCPZUUJCPZUUNYREPZUU
      JCPZUUPUUOUUQUURUUTUUJCUUQXQYQYSUUIUURUUTTXQUUMUTZXQYQYSUULVAZXQYQYSUULVC
      ZXQYTUUIUUKVDZYPYRUUHCEHIJVEVKVFUUQXQUUBUUIUUKUUSUUPTUVBUUQYOYQYSUUBXQYOU
      UMUUDVGZUVCUVDUUFVHUVEXQYTUUIUUKVIZUUAUUHUUJCEHIJVLVKUUQYOUUNHNZYSUUKUVAU
      UOTUVFUUQYOYQUUIUVHUVFUVCUVEYPUUHCEHIJUSVHUVDUVGUUNYRUUJCEHIJVJVKVMSXKXPX
      QVNXRXOHYBVOZUFQZXONZUVJYBMZHNXRXLDXOUHZUVIXPXKUVMXQXNXLDXOXLXMUTVPVQDXOH
      AYBYBWAZWBVRXOHUVJYBVSSXRXOHYDVOZUVKUVJYDMZHNXRXMDXOUHZUVOXPXKUVQXQXNXMDX
      OXLXMVTVPVQDXOHBYDYDWAZWBVRXOHUVJYDVSSXRDQZXTMZUVSYBMZUVSYDMZCPZTZDXOUHZU
      VKUVJXTMZUVLUVPCPZTZXPXKUWEXQXNUWDDXOUVSXONZXNUWDUWIXNOZUVTXSUWCUWIUVTXST
      ZXNUWIXSWCNUWKABCWDDXOXSWCXTXTWAZWEWFVGUWJUWAATZUWBBTZOUWCXSTUWIXLUWMXMUW
      NDXOAHYBUVNWEDXOBHYDUVRWEWGUWAAUWBBCWHVBWIWJWKVQUVKUWEUWHUWDUWHDUVJXODLLU
      WFUWGDLUVJXTDLXOXSWLYPUVJNDWMZWNDLUVLUVPCDLUVJYBDLXOAWLUWOWNYPCNDWMDLUVJY
      DDLXOBWLUWOWNWOWPUVSUVJTZUVTUWFUWCUWGUVSUVJXTWQUWPUWAUVLUWBUVPCUVSUVJYBWQ
      UVSUVJYDWQWRWSWTXASXDXKXPYAYKTXQKXTEFGXBXCXKXPYFYNTXQXKYCYLYEYMCUAYBEFGXB
      UBYDEFGXBWRXCXEXOXOXFZYAYGTXOXGZXSXOXODKXTEUWLXHXIUWQYFYJTUWRUWQYCYHYEYIC
      AXOXODUAYBEUVNXHBXOXODUBYDEUVRXHWRXIXJ $.
      $( [30-May-2014] $) $( [14-Sep-2010] $)
  $}

  ${
    clfsebs3.1 $e |- X = ran G $.
    ${
      $d G k $.  $d M k $.  $d N k $.  $d X k $.
      $( Closure of a finite composite of elements of the base set of an
         internal operation.  (When the operation is a monoid.)  (Contributed
         by FL, 22-Nov-2010.) $)
      clfsebs3 $p |- ( ( N e. ( ZZ>= ` M ) /\ G e. Mnd /\
                      A. k e. ( M ... N ) A e. X )
       -> prod_ k e. ( M ... N ) G A e. X ) $=
        ( cmnd wcel cuz cfv cmagm cexid cin cfz co wral cprd mndmgmid clfsebs2
        syl3an2 ) CHIEDJKICLMNIAFIBDEOPZQUBABCRFICSABCDEFGTUA $.
        $( [22-Nov-2010] $)
    $}

    ${
      $d G k $.  $d M k $.  $d N k $.  $d X k $.
      $( Closure of a finite composite of elements of the base set of an
         internal operation.  (When the operation is a group.)  (Contributed by
         FL, 22-Nov-2010.) $)
      clfsebs4 $p |- ( ( N e. ( ZZ>= ` M ) /\ G e. GrpOp /\
                      A. k e. ( M ... N ) A e. X )
       -> prod_ k e. ( M ... N ) G A e. X ) $=
        ( cgr wcel cuz cfv cmnd cfz co wral cprd grpomnd clfsebs3 syl3an2 ) CHI
        EDJKICLIAFIBDEMNZOTABCPFICQABCDEFGRS $.
        $( [22-Nov-2010] $)
    $}

    ${
      $d G k $.  $d M k $.  $d N k $.  $d X k $.
      $( Closure of a finite composite of elements of the base set of an
         internal operation.  (When the operation is an abelian group.)
         (Contributed by FL, 22-Nov-2010.) $)
      clfsebs5 $p |- ( ( N e. ( ZZ>= ` M ) /\ G e. AbelOp /\
                      A. k e. ( M ... N ) A e. X )
       -> prod_ k e. ( M ... N ) G A e. X ) $=
        ( cabl wcel cuz cfv cgr cfz co wral cprd ablogrpo clfsebs4 syl3an2 ) CH
        IEDJKICLIAFIBDEMNZOTABCPFICQABCDEFGRS $.
        $( [22-Nov-2010] $)
    $}
  $}

