$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              General Set Theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c x u v $.  $d a b x C $.  $d a b c x B $.  $d a b x u v A $.
    $d R a b u v x $.  $d C a b c x $.
    uninqs.1 $e |- Er R $.
    $( Class union distributes over the intersection of two subclasses of a
       quotient space.  Compare ~ uniin .  (Contributed by FL, 25-May-2007.) $)
    uninqs $p |- ( ( B C_ ( A /. R ) /\ C C_ ( A /. R ) ) ->
      U. ( B i^i C ) = ( U. B i^i U. C ) ) $=
      ( vx vc vb va vu vv cqs wss wa cin cuni wceq cv wcel wex wi eluni wal
      dfss2 pm3.35 cec wrex visset elqs eleq2 biimpa c0 wne inelcm wn df-ne wo
      erdisj pm5.61 eqeq12 biimprcd adantr sylbi mpan syl syl2an an4s exp32
      com24 pm2.43i ex r19.23aivw com3l com23 imp com13 imp31 eleq1 elin
      elequ2 biimprd wb incom eleq2i syl6bb equcoms biimpd anim12d a4imev
      expcom sylbir syl6bi syl9 com3r pm2.43a com14 com4t a4s com12 19.23aiv
      syl7bi syl5bi imp3a 3imtr4g ssrdv uniin jctil eqss sylibr ) BADLZMZCXJMZN
      ZBCOZPZBPZCPZOZMZXRXOMZNXOXRQXMXTXSXMFXRXOXMFRZXPSZYAXQSZNYAGRZSZYDXNSZNZ
      GTZYAXRSYAXOSXMYBYCYHYBYAHRZSZYIBSZNZHTZXMYCYHUAHYABUBYCYAIRZSZYNCSZNZITZ
      XMYMYHIYACUBYMXMYRYHUAZYLXMYSUAHYRXMYLYHYQXMYLYHUAZUAIXMYQYTXKXLYQYTUAZXK
      YKYIXJSZUAZHUCXLUUAUAZHBXJUDUUCUUDHXLUUCUUAXLYPYNXJSZUAZIUCUUCUUAUAZICXJU
      DUUFUUGIYQYLUUFUUCYHYOYPYLUUFUUCYHUAZUAZUAYLYPYOUUIYJYKYPYOUUIUAZUAZYJYKU
      UKUAYKYJYJUUKYJYKYJUUKUAZYKYJYKUULUAYPYKYJYKYJNZUUJYKYPYJUUMUUJUAZUAZYPYK
      YPUUOUAYPYKNZYJYPUUNUUPYJYPUUNUAYPUUMUUPYJNZUUJYPUUMUUQUUJUAYPUUMNZYOUUQU
      UIUURYOUUQUUIUAUURYONZUUFUUQUUHUUSUUFUUQUUHUAUUSUUFNUUCYNYIQZUUQYHUURYOUU
      FUUCUUTUAUURUUCUUFYOUUTYPUUMUUCUUFYOUUTUAZUAUAYPUUFUUCUUMUVAYPUUFUUCUUMUV
      AUAUAZYPUUFNUUEUVBYPUUEUEUUMUUCUUEUVAYKYJUUCUUEUVAUAZUAYKUUCYJUVCYKUUCYJU
      VCUAZYKUUCNUUBUVDYKUUBUEUUBYIJRZDUFZQZJAUGUVDJAYIDHUHUIUVGUVDJAUUEUVGYJUV
      AUUEYNKRZDUFZQZKAUGUVGYJUVAUAZUAZKAYNDIUHUIUVJUVLKAUVJUVGUVKUVJUVGNZUVKUV
      MYOYJUVMUUTUVMYOYJUVMUUTUAZUVJYOUVGYJUVNUVJYONYAUVISZYAUVFSZUVNUVGYJNUVJY
      OUVOYNUVIYAUJUKUVGYJUVPYIUVFYAUJUKUVOUVPNUVIUVFOZULUMZUVNYAUVIUVFUNUVRUVQ
      ULQZUOZUVNUVQULUPUVIUVFQZUVSUQZUVTUVNUVHUVEDKUHJUHEURUWBUVTNUWAUVTNUVNUWA
      UVSUSUWAUVNUVTUVMUUTUWAYNUVIYIUVFUTVAVBVCVDVCVEVFVGVHVIVJVKVLVCVMVLVCVEVK
      VNVOVPVEVKVIVOVIVQUUPYJUUTYHUAUUPUUTYJYHYPYKUUTYJYHUAZUAUUTYPYKUWCUUTYPYI
      CSZYKUWCUAYNYICVRUWDYKUWCUWDYKNYICBOZSZUWCYICBVSYJUWFYHYJUWFNYGGHYDYIQZYJ
      YEUWFYFUWGYEYJGHFVTWAUWGUWFYFUWFYFWBHGYIYDQUWFYDUWESYFYIYDUWEVRUWEXNYDCBW
      CWDWEWFWGWHWIWJWKVKWLVMVOVNVOWMVKVNVKVNVKWNVKVNVKWOWPVKWOVPVJVOVPVOWQWRVC
      WSWRVCVOWSWTVPWTWSXAXBXCYAXPXQVSGYAXNUBXDXEBCXFXGXOXRXHXI $.
      $( [4-Jun-2007] $) $( [25-May-2007] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Inference from membership to difference.  (Contributed by FL,
       6-Jun-2007.)  (The proof was shortened by Andrew Salmon, 9-Jul-2011.) $)
    difeqri2 $p |- ( A. x ( ( x e. A /\ -. x e. B ) <-> x e. C )
      -> ( A \ B ) = C ) $=
      ( cv wcel wn wa wb wal cdif wceq eldif bibi1i albii dfcleq biimpri
      sylbir ) AEZBFSCFGHZSDFZIZAJSBCKZFZUAIZAJZUCDLZUEUBAUDTUASBCMNOUGUFAUCDPQ
      R $.
      $( [9-Jul-2011] $) $( [6-Jun-2007] $)

  $}

  ${
    $d A u v w y z $.  $d B u v w y z $.  $d C u v w y z $.  $d D u v w y z $.
    $d ph u x $.  $d ta u v w y z $.  $d v w x y z $.
    elo.1 $e |- ( y = A -> ( ph <-> ps ) ) $.
    elo.2 $e |- ( z = B -> ( ps <-> ch ) ) $.
    elo.3 $e |- ( v = C -> ( ch <-> th ) ) $.
    elo.4 $e |- ( w = D -> ( th <-> ta ) ) $.
    $( The law of concretion for operation class abstraction.  Compare with
       ~ eloprabg .  This version is to be used with categories.  (Contributed
       by FL, 14-Jul-2007.) $)
    elo $p |- ( ( ( A e. Q /\ B e. R /\ C e. S ) /\ D e. T ) ->
      ( <. <. A , B >. , <. C , D >. >. e. { x | E. y E. z E. v E. w
      ( x = <. <. y , z >. , <. v , w >. >. /\ ph ) } <-> ta ) ) $=
      ( vu wcel w3a cvv cop cv wceq wa wex cab wb elisset 3anim123i wi opex
      eqeq1 eqcom syl6bb cxp simpl2 opelxpi 3ad2antl3 simpr visset otthg
      3adant3 opthg 3anbi3d and4as 3ad2ant3 bitrd syl111anc sylan9bbr anbi1d
      syl3an9b sylan9bb pm5.32i 4exbidv elab eleq1 syl5bbr adantl elex anim12i
      eeeeanv sylibr biantrurd 19.41vvvv syl6rbbr adantr 3bitr3d expcom vtocle
      syl2an ) KOUDZLPUDZMQUDZUEKUFUDZLUFUDZMUFUDZUEZNUFUDZKLUGZMNUGZUGZFUHZGUH
      ZHUHZUGJUHZIUHZUGZUGZUIZAUJZIUKJUKHUKGUKZFULZUDZEUMZNRUDWQWTWRXAWSXBKOUNL
      PUNMQUNUONRUNXCXDUJZXTUPUCXGXEXFUQYAUCUHZXGUIZXTYAYCUJZYBXNUIZAUJZIUKJUKH
      UKGUKZXIKUIZXJLUIZXKMUIZUEZXLNUIZUJZEUJZIUKJUKHUKGUKZXSEYDYFYNGHJIYDYFYMA
      UJYNYDYEYMAYCYEXNXGUIZYAYMYCYEXGXNUIYPYBXGXNURXGXNUSUTYAXAXFUFUFVAZUDZXDY
      PYMUMWTXAXBXDVBXBWTXDYRXAMNUFUFVCVDXCXDVEXAYRXDUEYPYHYIXMXFUIZUEZYMXAYRYP
      YTUMXDXIXJKLXMXFUFYQGVFHVFXKXLUQVGVHXDXAYTYMUMYRXDYTYHYIYJYLUJZUEYMXDYSUU
      AYHYIXKXLMNUFJVFIVFVIVJYHYIYJYLVKUTVLVMVNVOVPYMAEYKADYLEYHABYICYJDSTUAVQU
      BVRVSUTVTYCYGXSUMYAYGYBXRUDYCXSXQYGFYBUCVFXHYBUIZXPYFGHJIUUBXOYEAXHYBXNUR
      VPVTWAYBXGXRWBWCWDYAYOEUMYCYAEYMIUKJUKHUKGUKZEUJYOYAUUCEYAYHGUKZYIHUKZYJJ
      UKZUEZYLIUKZUJUUCXCUUGXDUUHWTUUDXAUUEXBUUFGKUFWEHLUFWEJMUFWEUOINUFWEWFYHY
      IYJYLHJIGWGWHWIYMEHJIGWJWKWLWMWNWOWP $.
      $( [14-Jul-2007] $)
  $}

  ${
    $d a b x y A $.  $d a b x y B $.  $d a b x y C $.
    $( The intersection of two finite intersections is finite.  (Contributed by
       FL, 31-Oct-2007.) $)
    infi1 $p |- ( ( A e. { x | E. y ( y C_ C /\ y e. Fin /\ x = |^| y ) }
                    /\ B e. { x | E. y ( y C_ C /\ y e. Fin /\ x = |^| y ) } )
        -> ( A i^i B ) e. { x | E. y ( y C_ C /\ y e. Fin /\ x = |^| y ) } ) $=
      ( va vb cv wss cfn wcel cint wceq w3a wex cab wa cin cvv inex1g adantr
      wi spfi sseq1 eleq1 inteq eqeq2d 3anbi123d cbvexv cun visset unex an6
      unss biimpi unfi ineq2 ineq1 adantl incom intun eqtr4i a1i 3eqtrd
      3anim123i sylbi cla4egv mpsyl expcom 19.23aiv com12 imp syl2anb 3adant3
      wb bicomd 3ad2ant3 mpbid 3exp syl6bi imp31 an4s ex mpid pm2.43i ) CBHZEIZ
      WFJKZAHWFLZMNBOAPZKZDWJKZQZCDRZWJKZWMWMWNSKZWOWKWPWLCDWJTUAWMWMWPWOUBZWKW
      KWLWLWQWKWKWLWLQZWQWKWKWGWHCWIMZNZBOZWRWQUBABCWJEUCWRXAWQWLWLXAWQUBZWLWLW
      GWHDWIMZNZBOZXBABDWJEUCXEXAWPWOXEXAWPNWGWHWNWIMZNZBOZWOXEXAXHWPXEFHZEIZXI
      JKZDXILZMZNZFOZGHZEIZXPJKZCXPLZMZNZGOZXHXAXDXNBFWFXIMZWGXJWHXKXCXMWFXIEUD
      WFXIJUEYCWIXLDWFXIUFUGUHUIWTYABGWFXPMZWGXQWHXRWSXTWFXPEUDWFXPJUEYDWIXSCWF
      XPUFUGUHUIXOYBXHXNYBXHUBFYBXNXHYAXNXHUBGXNYAXHXIXPUJZSKXNYAQZYEEIZYEJKZWN
      YELZMZNZXHXIXPFUKGUKULYFXJXQQZXKXRQZXMXTQZNYKXJXKXMXQXRXTUMYLYGYMYHYNYJYL
      YGXIXPEUNUOXIXPUPYNWNCXLRZXSXLRZYIXMWNYOMXTDXLCUQUAXTYOYPMXMCXSXLURUSYPYI
      MYNYPXLXSRYIXSXLUTXIXPVAVBVCVDVEVFXGYKBYESWFYEMZWGYGWHYHXFYJWFYEEUDWFYEJU
      EYQWIYIWNWFYEUFUGUHVGVHVIVJVKVJVLVMVNWPXEXHWOVOXAWPWOXHABWNSEUCVPVQVRVSVT
      VLVKVTWAWBWCWDWE $.
      $( [31-Oct-2007] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( If ` A ` is the intersection of a finite set of elements of ` B ` then
       ` A C_ U. B ` .  (Contributed by FL, 30-Nov-2007.) $)
    fiiu $p |- ( A e. { x | E. y ( y C_ B /\ y e. Fin /\ x = |^| y ) }
           -> A C_ U. B ) $=
      ( cv wss cfn wcel cint wceq w3a wex cab cuni cvv wn nvel eleq1 biimpcd
      mtoi wi spfi eqeq1 notbid biimpd c0 int0 eqeq2 bicomd inteq con3i wne
      df-ne intssuni2 sylan2br ancoms 3adant2 sseq1 eqcoms syl5com 3exp com24
      syl syl6bi ax-mp syl6com com13 pm2.43i com4t 3imp 19.23aiv mpid ) CBEZDFZ
      VMGHZAEVMIZJKBLAMZHZCDNZFZVRVRCOJZPZVTVRWAOVQHZVQQWAVRWCCOVQRSTVRVRVNVOCV
      PJZKZBLWBVTUAZABCVQDUBWEWFBVNVOWDWFWDWBVNVOVTWDWBVNVOVTUAUAZUAWBWDWDWGWDW
      BVPOJZPZWDWGUAZWDWBWIWDWAWHCVPOUCUDUEUFIZOJZWIWJUAUGWLWIVPWKJZPZWJWLWHWMW
      LWMWHWKOVPUHUIUDWNVMUFJZPZWJWOWMVMUFUJUKWPVOVNWDVTWPVOVNWDVTUAWPVOVNKVPVS
      FZWDVTWPVNWQVOVNWPWQWPVNVMUFULWQVMUFUMVMDUNUOUPUQWQVTUAVPCVPCJWQVTVPCVSUR
      UEUSUTVAVBVCVDVEVFVGVHVIVJVKVDVLVH $.
      $( [30-Nov-2007] $)
  $}


  $( An intersection with a member of a powerset belongs to this powerset.
     (Contributed by FL, 25-Sep-2007.) $)
  inpws1 $p |- ( A e. ~P C -> ( A i^i B ) e. ~P C ) $=
    ( cin cvv wcel cpw inex1g wss elpwi ssinss1 syl elpwg syl5ibr mpcom ) ABDZE
    FZACGZFZPRFZABRHSTQPCIZSACIUAACJABCKLPCEMNO $.
    $( [25-Sep-2007] $)

  $( An intersection with a member of a powerset belongs to this powerset.
     (Contributed by FL, 26-Oct-2007.) $)
  inpws2 $p |- ( B e. ~P C -> ( A i^i B ) e. ~P C ) $=
    ( cpw wcel cin incom inpws1 syl5eqel ) BCDZEABFBAFJABGBACHI $.
    $( [26-Oct-2007] $)

  ${
    $d v x $.  $d w x $.  $d x y $.  $d x z $.
    $( Structure of the class abstraction used by ` Alg ` , ` Cat ` and
       ` Ded ` .  (Contributed by FL, 26-Oct-2007.) $)
    stcat $p |- { x | E. y E. z E. v E. w ( x = <. <. y , z >. , <. v , w >. >.
       /\ ph ) } C_ ( ( _V X. _V ) X. ( _V X. _V ) ) $=
      ( cv cop wceq wa wex cvv cxp wcel opex opelxp visset mpbir2an eleq1
      mpbiri adantr 19.23aivv abssi ) BGZCGZDGZHZFGZEGZHZHZIZAJZEKFKZDKCKBLLMZU
      OMZUNUDUPNZCDUMUQFEULUQAULUQUKUPNZURUGUONZUJUONZUGUJUOUOUHUIOPUSUELNUFLNU
      EUFLLDQZPCQVARUTUHLNUILNUHUILLEQZPFQVBRRUDUKUPSTUAUBUBUC $.
      $( [26-Oct-2007] $)
  $}

  $( A theorem of the ~ 1st2nd family.  (Contributed by FL, 26-Oct-2007.) $)
  11st22nd $p |- ( ( ( Rel B /\ Rel dom B /\ Rel ran B ) /\ A e. B ) ->
     A = <. <. ( 1st ` ( 1st ` A ) ) , ( 2nd ` ( 1st ` A ) ) >. ,
            <. ( 1st ` ( 2nd ` A ) ) , ( 2nd ` ( 2nd ` A ) ) >. >. ) $=
    ( wrel cdm crn w3a wcel wa c1st cfv c2nd cop wceq 1st2nd 3ad2antl1 wi
    1stdm sylan2 exp32 a1i com13 3imp1 2ndrn com3r opeq12d eqtrd ) BCZBDZCZBEZC
    ZFABGZHZAAIJZAKJZLZUNIJUNKJLZUOIJUOKJLZLUGUIULAUPMUKABNOUMUNUQUOURUGUIUKULU
    NUQMZUKUIUGULUSPZUIUGUTPPUKUIUGULUSUGULHZUIUNUHGUSABQUNUHNRSTUAUBUGUIUKULUO
    URMZUIUKUGULVBPZUKUGVCPPUIUKUGULVBVAUKUOUJGVBABUCUOUJNRSTUDUBUEUF $.
    $( [26-Oct-2007] $)

  ${
    $d A x $.
    $( The union of a part of a powerset belongs to it.  (Contributed by FL,
       16-Nov-2007.) $)
    ump $p |- ( A e. V -> U. { x e. ~P A | ph } e. ~P A ) $=
      ( wcel cpw crab cuni wss ssrab2 sspwuni mpbi elpw2g mpbiri ) CDEABCFZGZHZ
      OEQCIZPOIRABOJPCKLQCDMN $.
      $( [29-Nov-2007] $) $( [16-Nov-2007] $)
  $}

  $( Moving an element ` B ` out from the intersection of a class ` A ` .
     (Contributed by FL, 29-Nov-2007.) $)
  moec $p |- ( B e. A -> |^| A = ( B i^i |^| ( A \ { B } ) ) ) $=
    ( wcel cint csn cdif cun cin difsnid eqcomd inteqd wceq intun a1i cvv
    elisset c0 cif sneq id eqeq12d 0ex elimel intsn dedth syl ineq2d incom
    syl6eq 3eqtrd ) BACZADABEZFZULGZDZUMDZULDZHZBUPHZUKAUNUKUNAABIJKUOURLUKUMUL
    MNUKURUPBHUSUKUQBUPUKBOCZUQBLZBAPUTVAUTBQRZEZDZVBLBQBVBLZUQVDBVBVEULVCBVBSK
    VETUAVBBQOUBUCUDUEUFUGUPBUHUIUJ $.
    $( [29-Nov-2007] $)

  ${
    $d A x y $.  $d B x y $.  $d C y $.
    $( Splitting an intersection.  (Contributed by FL, 15-Oct-2012.) $)
    splint $p |- ( B C_ A ->
    |^|_ x e. A C = ( |^|_ x e. ( A \ B ) C i^i |^|_ x e. B C ) ) $=
      ( vy wss cv ciin wcel cdif cin wb wal wceq wral wa cvv visset eliin
      ax-mp a1i cun undif equncom biimpi eqcoms sylbi raleqdv ralunb syl6bb
      bicomi bicomd anbi12d elin syl6bbr 3bitrd 19.21aiv dfcleq sylibr ) CBFZEG
      ZABDHZIZVAABCJZDHZACDHZKZIZLZEMVBVGNUTVIEUTVCVADIZABOZVJAVDOZVJACOZPZVHVC
      VKLZUTVAQIZVOERZAVABDQSTUAUTVKVJAVDCUBZOVNUTVJABVRUTCVDUBZBNBVRNZCBUCVTBV
      SBVSNVTBCVDUDUEUFUGUHVJAVDCUIUJUTVNVAVEIZVAVFIZPVHUTVLWAVMWBVLWALUTWAVLVP
      WAVLLVQAVAVDDQSTUKUAUTWBVMWBVMLZUTVPWCVQAVACDQSTUAULUMVAVEVFUNUOUPUQEVBVG
      URUS $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d A x $.  $d B x $.  $d D x $.
    splintx.1 $e |- ( x = B -> C = D ) $.
    $( Moving an element out from an intersection.  (Contributed by FL,
       15-Oct-2012.) $)
    splintx $p |- ( B e. A ->
     |^|_ x e. A C = ( |^|_ x e. ( A \ { B } ) C i^i D ) ) $=
      ( wcel ciin csn cdif cin wss wceq snssi splint syl iinxsng ineq2d eqtrd
      ) CBGZABDHZABCIZJDHZAUBDHZKZUCEKTUBBLUAUEMCBNABUBDOPTUDEUCACDEBFQRS $.
      $( [15-Oct-2012] $)
  $}

  ${
    $( A function's value belongs to its range.  A more general version of
       ~ fnoprvrn .  To be used with partial operations.  (Contributed by FL,
       10-Mar-2008.) $)
    fnoprvrn2 $p |- ( ( Fun F /\ <. A , B >. e. dom F ) ->
      ( A F B ) e. ran F ) $=
      ( wfun cop cdm wcel wa co cfv crn df-opr fvelrn syl5eqel ) CDABEZCFGHABCI
      OCJCKABCLOCMN $.
      $( [10-Mar-2008] $)
  $}

  ${
    $d A a b x y $.  $d B a b x y $.  $d L a b x y $.
    ficli.1 $e |- F = { x | E. y ( y C_ L /\ y e. Fin /\ x = |^| y ) } $.
    $( The class of finite intersections of a class ` L ` are closed under
       intersection.  (Contributed by FL, 27-Apr-2008.) $)
    ficli $p |- ( ( A e. F /\ B e. F ) -> ( A i^i B ) e. F ) $=
      ( va vb wcel wa cin cv wss cfn cint wceq w3a wex cab eleq2i spfi biimpd
      im2anan9 sseq1 eleq1 inteq eqeq2d 3anbi123d cbvexv wi cpw wrex cun an6
      unss biimpi ancoms visset unex elpw sylibr unfi ineq12 intun syl6eqr
      anim12i 3impb sylbi anbi12d rcla4ev syl bicomi 3anbi1i 3anass bitri
      exbii df-rex bitr4i ex 19.23aiv com12 imp syl2anb syl6 pm2.43i cvv wb
      inex1g adantr syl5bb mpbird ) CEJZDEJZKZCDLZEJZBMZFNZWROJZWPWRPZQZRZBSZWM
      CWSWTAMXAQRBSATZJZDXEJZXDWNEXECGUAEXEDGUAXFXGKZXDXHXHWSWTCXAQZRZBSZWSWTDX
      AQZRZBSZKXDXFXFXKXGXGXNXFXFXKABCXEFUBUCXGXGXNABDXEFUBUCUDXKHMZFNZXOOJZCXO
      PZQZRZHSZIMZFNZYBOJZDYBPZQZRZISZXDXNXJXTBHWRXOQZWSXPWTXQXIXSWRXOFUEWRXOOU
      FYIXAXRCWRXOUGUHUIUJXMYGBIWRYBQZWSYCWTYDXLYFWRYBFUEWRYBOUFYJXAYEDWRYBUGUH
      UIUJYAYHXDXTYHXDUKHYHXTXDYGXTXDUKIYGXTXDYGXTKZWTXBKZBFULZUMZXDYKXOYBUNZYM
      JZYOOJZWPYOPZQZKZKZYNYKYCXPKZYDXQKZYFXSKZRUUAYCYDYFXPXQXSUOUUBUUCUUDUUAUU
      BYPUUCUUDKYTUUBYOFNZYPXPYCUUEXPYCKUUEXOYBFUPUQURYOFXOYBHUSIUSUTVAVBUUCYQU
      UDYSXQYDYQXOYBVCURXSYFYSXSYFKWPXRYELYRCXRDYEVDXOYBVEVFURVGVGVHVIYLYTBYOYM
      WRYOQZWTYQXBYSWRYOOUFUUFXAYRWPWRYOUGUHVJVKVLXDWRYMJZYLKZBSYNXCUUHBXCUUGWT
      XBRUUHWSUUGWTXBUUGWSWRFBUSVAVMVNUUGWTXBVOVPVQYLBYMVRVSVBVTWAWBWAWCWDWEWFW
      DWQWPXEJZWOXDEXEWPGUAWOWPWGJZUUIXDWHWMUUJWNCDEWIWJABWPWGFUBVLWKWL $.
      $( [27-Apr-2008] $)
  $}

  $( If an intersection is not empty its operands are not empty.  (Contributed
     by FL, 27-Apr-2008.) $)
  neiopne $p |- ( ( A i^i B ) =/= (/) -> ( A =/= (/) /\ B =/= (/) ) ) $=
    ( cin c0 wceq wn wo wne wa ineq1 incom eqtr in0 syl6eq mpan2 syl ineq2
    jaoi con3i df-ne neanior 3imtr4i ) ABCZDEZFADEZBDEZGZFUCDHADHBDHIUGUDUEUDUF
    UEUCDBCZEZUDADBJUIUHBDCZEZUDDBKUIUKIUCUJDUCUHUJLBMNOPUFUCADCDBDAQAMNRSUCDTA
    DBDUAUB $.
    $( [27-Apr-2008] $)

  $( Image of a pre-image.  (Contributed by FL, 1-Jun-2008.) $)
  f2imacnv $p |- ( ( F : A -1-1-onto-> B /\ C C_ B ) ->
                 ( F " ( `' F " C ) ) = C ) $=
    ( wf1o wss ccnv cima wceq wf1 wi f1ocnv f1of1 wa imacnvcnv f1imacnv
    syl5eqr ex 3syl imp ) ABDEZCBFZDDGZCHZHZCIZUABAUCEBAUCJZUBUFKABDLBAUCMUGUBU
    FUGUBNUEUCGUDHCDUDOBACUCPQRST $.
    $( [1-Jun-2008] $)


  $( Symmetrical equality of the images and of their antecedents when the
     mapping is one to one.  (Contributed by FL, 1-Jun-2008.) $)
  oooeqim2 $p |- ( ( F : A -1-1-> B /\ X C_ A /\ Y C_ A )
     -> ( ( F " X ) = ( F " Y ) <-> X = Y ) ) $=
    ( wf1 wss w3a cima wceq ccnv wa f1imacnv ex anim12d 3impib imaeq2 wi eqtr
    syl com23 eqcoms imp syl2im impbid1 ) ABCFZDAGZEAGZHZCDIZCEIZJZDEJZUICKZUJI
    ZDJZUNUKIZEJZLZULUOUQJZUMUFUGUHUSUFUGUPUHURUFUGUPABDCMNUFUHURABECMNOPUJUKUN
    QUPURUTUMRZURVARDUODUOJZUTURUMVBUTURUMRZVBUTLDUQJZVCDUOUQSVDURUMDUQESNTNUAU
    BUCUDDECQUE $.
    $( [1-Jun-2008] $)

  $( A theorem about things which don't exist ` _V ` and ` ( _V X. _V ) ` .
     (Contributed by FL, 22-Sep-2008.) $)
  vxveqv $p |- ( _V X. _V ) =/= _V $=
    ( vx cvv cxp wne wceq wn wpss wss dfpss3 ssv cv wcel wa wex c0 0ex 0nelxp
    eleq1 notbid anbi12d cla4ev mp2an nss mpbir mpbir2an pssv mpbi df-ne ) BBCZ
    BDUIBEFZUIBGZUJUKUIBHBUIHFZUIBIUIJULAKZBLZUMUILZFZMZANZOBLZOUILZFZURPBBQUQU
    SVAMAOPUMOEZUNUSUPVAUMOBRVBUOUTUMOUIRSTUAUBABUIUCUDUEUIUFUGUIBUHUD $.
    $( [23-Sep-2008] $) $( [22-Sep-2008] $)

  $( The double union of the converse of a class ` A ` is included in the
     double union of the class.  (Contributed by FL, 31-Jul-2009.) $)
  ducidu $p |- U. U. `' A C_ U. U. A $=
    ( ccnv cuni cdm crn cun unidmrn dmrnssfld eqsstri ) ABCCADAEFACCAGAHI $.
    $( [31-Jul-2009] $)


  $( The field of a class equals the field of the its converse.  (Contributed
     by FL, 16-Apr-2012.) $)
  fldcnv $p |- ( dom A u. ran A ) = ( dom `' A u. ran `' A ) $=
    ( cdm crn cun ccnv uncom df-rn dfdm4 uneq12i eqtri ) ABZACZDLKDAEZBZMCZDKLF
    LNKOAGAHIJ $.
    $( [16-Apr-2012] $)

  $( if ` R ` is a relation, its double union equals the double union of its
     converse.  (Contributed by FL, 5-Jan-2009.) $)
  relcnvfld $p |- ( Rel R -> U. U. R = U. U. `' R ) $=
    ( wrel cuni cdm crn cun ccnv relfld unidmrn syl6eqr ) ABACCADAEFAGCCAHAIJ
    $.
    $( [5-Jan-2009] $)

  ${
    $d R x $.
    $( The domain of a reflexive class equals its field.  (Contributed by FL,
       29-Dec-2011.) $)
    domfldrefc $p |- ( A. x e. ( dom R u. ran R ) x R x
      -> dom R = ( dom R u. ran R ) ) $=
      ( cv wbr cdm crn cun wral wss wa wceq wcel wi wal visset breldm ralimi
      df-ral sylib dfss2 sylibr ssun1 jctil eqss ) ACZUEBDZABEZBFZGZHZUGUIIZUIU
      GIZJUGUIKUJULUKUJUEUILUEUGLZMANZULUJUMAUIHUNUFUMAUIUEUEBAOPQUMAUIRSAUIUGT
      UAUGUHUBUCUGUIUDUA $.
      $( [29-Dec-2011] $)

    $( The range of a reflexive class equals its field.  (Contributed by FL,
       29-Dec-2011.) $)
    ranfldrefc $p |- ( A. x e. ( dom R u. ran R ) x R x
      -> ran R = ( dom R u. ran R ) ) $=
      ( cv wbr cdm crn cun wral wss wa wceq wcel wi wal visset brelrn ralimi
      df-ral sylib dfss2 sylibr ssun2 jctil eqss ) ACZUEBDZABEZBFZGZHZUHUIIZUIU
      HIZJUHUIKUJULUKUJUEUILUEUHLZMANZULUJUMAUIHUNUFUMAUIUEUEBAOZUOPQUMAUIRSAUI
      UHTUAUHUGUBUCUHUIUDUA $.
      $( [29-Dec-2011] $)

    $( The domain and range of a reflexive class are equal.  (Contributed by
       FL, 29-Dec-2011.) $)
    dranfldrefc $p |- ( A. x e. ( dom R u. ran R ) x R x
      -> dom R = ran R ) $=
      ( cv wbr cdm crn cun wral domfldrefc ranfldrefc eqtr4d ) ACZLBDABEZBFZGZH
      MONABIABJK $.
      $( [29-Dec-2011] $)
  $}

  ${
    $d R x $.
    $( Domain and range of a reflexive relation are equal.  (Contributed by FL,
       6-Oct-2008.) $)
    domrngref $p |- ( ( Rel R /\ A. x e. U. U. R x R x ) -> dom R = ran R ) $=
      ( wrel cv wbr cuni wral wa cdm wcel crn wb wal wceq wi df-ral cun relfld
      eqcomd eleq2d biimpd imim1d wo elun imbi1i jaob visset brelrn imim2i
      adantr breldm adantl impbid sylbi syl6 alimdv syl5bi imp dfcleq sylibr )
      BCZADZVBBEZABFFZGZHVBBIZJZVBBKZJZLZAMZVFVHNVAVEVKVEVBVDJZVCOZAMVAVKVCAVDP
      VAVMVJAVAVMVBVFVHQZJZVCOZVJVAVOVLVCVAVOVLVAVNVDVBVAVDVNBRSTUAUBVPVGVIUCZV
      COZVJVOVQVCVBVFVHUDUEVRVGVCOZVIVCOZHZVJVGVCVIUFWAVGVIVSVGVIOVTVCVIVGVBVBB
      AUGZWBUHUIUJVTVIVGOVSVCVGVIVBVBBWBUKUIULUMUNUNUOUPUQURAVFVHUSUT $.
      $( [7-Oct-2008] $) $( [6-Oct-2008] $)

    $( The domain of a reflexive relation is equal to its field .  (Contributed
       by FL, 6-Oct-2008.) $)
    domfldref $p |- ( ( Rel R /\ A. x e. U. U. R x R x ) -> dom R = U. U. R )
        $=
      ( wrel cv wbr cuni wral cdm wceq crn cun wa relfld wi domrngref uneq2
      eqcomd eqtr unidm syl6req ex syl5com syl anabsi5 ) BCZADZUFBEABFFZGZBHZUG
      IZUEUGUIBJZKZIZUEUHLZUJBMUNUIUKIZUMUJNABOUOULUIUIKZIZUMUJUOUPULUIUKUIPQUM
      UQUJUMUQLUGUPUIUGULUPRUISTUAUBUCUBUD $.
      $( [7-Oct-2008] $) $( [6-Oct-2008] $)
  $}

  ${
    $d A x $.  $d R x $.
    $( In a reflexive class ` R ` , an element ` A ` belongs to the field iff
       the pair ` <. A , A >. ` belongs to ` R ` .  (Contributed by FL,
       30-Dec-2011.) $)
    domintreflemb $p |- ( ( A e. B /\ A. x e. dom R x R x ) ->
      ( A e. dom R <-> A R A ) ) $=
      ( wcel cv wbr cdm wral wa wi wceq id breq12d rcla4cva ex adantl breldmg
      adantr impbid ) BCEZAFZUBDGZADHZIZJBUDEZBBDGZUEUFUGKUAUEUFUGUCUGABUDUBBLZ
      UBBUBBDUHMZUINOPQUAUGUFKUEUAUGUFBBCDRPST $.
      $( [30-Dec-2011] $)
  $}

  ${
    $d R x $.  $d S x $.
    $( The domain of the intersection of two reflexive classes is the
       intersection of their domains.  Compare with ~ dmin .  (Contributed by
       FL, 30-Dec-2011.) $)
    domintrefb $p |- ( ( A. x e. dom R x R x /\
     A. x e. dom S x S x ) ->
      dom ( R i^i S ) = ( dom R i^i dom S ) ) $=
      ( cv wbr cdm wral wa cin wss wceq wcel inss1 sseli imim1i ralimi2 inss2
      r19.26 brin visset breldm sylbir ralimi dfss3 sylibr syl2an dmin jctil
      eqss ) ADZUJBEZABFZGZUJUJCEZACFZGZHZBCIZFZULUOIZJZUTUSJZHUSUTKUQVBVAUMUKA
      UTGZUNAUTGZVBUPUKUKAULUTUJUTLZUJULLUKUTULUJULUOMNOPUNUNAUOUTVEUJUOLUNUTUO
      UJULUOQNOPVCVDHUKUNHZAUTGZVBUKUNAUTRVGUJUSLZAUTGVBVFVHAUTVFUJUJUREVHUJUJB
      CSUJUJURATUAUBUCAUTUSUDUEUBUFBCUGUHUSUTUIUE $.
      $( [30-Dec-2011] $)
  $}

  ${
    $d A x y $.  $d R x y $.
    $( If ` R ` is a reflexive relation and ` A ` a part of its field, ` A ` is
       a part of the image of ` A ` by ` R ` .  (Contributed by FL,
       3-Jul-2009.) $)
    imgfldref2 $p |- ( ( A. x e. U. U. R x R x /\ A C_ U. U. R )
    -> A C_ ( R " A ) ) $=
      ( vy cv wbr cuni wral wss wa wrex cab cima wcel wi wal hbra1 ax-17 hban
      ssel ra4 breq1 rcla4e expcom syl6com com24 pm2.43i com3l imp 19.21ai
      ssab sylibr dfima2 syl6sseqr ) AEZUOCFZACGGZHZBUQIZJZBDEZUOCFZDBKZALZCBMU
      TUOBNZVCOZAPBVDIUTVFAURUSAUPAUQQUSARSURUSVFVEURUSVCVEURUSVCOOVEUSURVEVCUS
      VEUOUQNZURVFOBUQUOTURVGUPVFUPAUQUAVEUPVCVBUPDUOBUPDRVAUOUOCUBUCUDUEUEUFUG
      UHUIUJVCABUKULDACBUMUN $.
      $( [3-Jul-2009] $)
  $}


  $( ` <. A , A >. ` belongs to a restriction of the identity class iff ` A `
     belongs to the restricting class.  (Contributed by FL, 27-Oct-2008.) $)
  eltids $p |- ( A e. V -> ( A e. B <-> <. A , A >. e. ( _I |` B ) ) ) $=
    ( wcel cop cid wa cres wbr ididg df-br sylib biantrurd opelresg bitr4d ) AC
    DZABDZAAEZFDZQGRFBHDPSQPAAFISACJAAFKLMAAFBCNO $.
    $( [17-Nov-2008] $) $( [27-Oct-2008] $)

  ${
    $d A u x y $.  $d R u x y $.
    $( Two ways to state a relation is reflexive.  (Adapted from Tarski.)
       (Contributed by FL, 15-Jan-2012.) $)
    ref4w $p |- ( A. x e. A x R x
     <-> ( _I |` A ) C_ R ) $=
      ( vy vu cv wbr wral wcel wi wal cop cid cres wss df-ral cvv wb visset
      eltids ax-mp df-br imbi12i albii ralidm ralv bitri cxp pm2.27 wa
      opelresg weq ideq opeq2 eleq1d biimpcd syl6 syl6bi pm2.43i com3r sylbi
      sylbir imp r19.21aiv a4s ralimi wceq eleq1 imbi12d ralxp sylibr wrel
      relres df-rel mpbi sseli ancri pm3.31 syl5 alimi syl dfss2 ssel 19.21aiv
      impbii 3bitri ) AFZWGCGZABHWGBIZWHJZAKWGWGLZMBNZIZWKCIZJZAKZWLCOZWHABPWJW
      OAWIWMWHWNWGQIZWIWMRASZWGBQTUAWGWGCUBUCUDWPWQWPDFZWLIZWTCIZJZDKZWQWPWOAQH
      ZAQHZXDXFXEWPWOAQUEWOAUFUGXFXCDQQUHZHZXDXFWGEFZLZWLIZXJCIZJZEQHZAQHXHXEXN
      AQXEWRWOJZAKXNWOAQPXOXNAWRXOXNJWSWRXOWOXNWRWOUIWOXMEQXIQIZXKWOXLXPXKXJMIZ
      WIUJWOXLJZWGXIMBQUKXQWIXRXQWGXIMGZWIXRJZWGXIMUBXSAEULZXTWGXIESUMWIWOYAXLW
      IWOYAXLJZJZWIWIWMYCWGBBTWMWOWNYBWMWNUIYAWNXLYAWKXJCWGXIWGUNUOUPUQURUSUTVA
      VBVCURUTVDUQUAVEVAVFXCXMDAEQQWTXJVGXAXKXBXLWTXJWLVHWTXJCVHVIVJVKXHWTXGIZX
      CJZDKXDXCDXGPYEXCDXAYDXAUJYEXBXAYDWLXGWTWLVLWLXGOMBVMWLVNVOVPVQYDXAXBVRVS
      VTVAWAVBDWLCWBVKWQWOAWLCWKWCWDWEWF $.
      $( [15-Jan-2012] $)
  $}

  ${
    $d R x $.
    $( The converse of a reflexive class is reflexive.  (Contributed by FL,
       31-Jul-2009.) $)
    cnvref $p |- ( A. x e. ( dom R u. ran R ) x R x <->
    A. x e. ( dom `' R u. ran `' R ) x `' R x ) $=
      ( cv wbr cdm crn cun wral ccnv wcel wb wa brcnvg bicomd anidms ralbiia
      cuni unidmrn eqcomi wrel wceq relcnv relfld ax-mp eqtri raleqi bitri ) AC
      ZUHBDZABEBFGZHUHUHBIZDZAUJHULAUKEUKFGZHUIULAUJUHUJJZUIULKUNUNLULUIUHUHUJU
      JBMNOPULAUJUMUJUKQQZUMUOUJBRSUKTUOUMUABUBUKUCUDUEUFUG $.
      $( [31-Jul-2009] $)
  $}

  ${
    $d R x $.
    $( The converse of a reflexive relation is reflexive.  (Contributed by FL,
       19-Sep-2011.) $)
    cnvref2 $p |- ( Rel R -> ( A. x e. U. U. R x R x <->
    A. x e. U. U. `' R x `' R x ) ) $=
      ( wrel cv wbr cuni wral cdm crn cun ccnv relfld raleqdv wb cnvref a1i
      wceq relcnv eqcomd syl 3bitrd ) BCZADZUCBEZABFFZGUDABHBIJZGZUCUCBKZEZAUHH
      UHIJZGZUIAUHFFZGUBUDAUEUFBLMUGUKNUBABOPUBUIAUJULUBUHCZUJULQUMUBBRPUMULUJU
      HLSTMUA $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d R y $.  $d x y $.
    $( Strong reflexivity implies weak reflexivity.  (Strong and weak
       reflexivity is the difference between a toset and a poset).
       (Contributed by FL, 29-Dec-2011.) $)
    srefwref $p |- ( A. x e. ( dom R u. ran R ) A. y e. ( dom R u. ran R )
   ( x R y \/ y R x ) -> A. x e. ( dom R u. ran R ) x R x ) $=
      ( cv wbr wo cdm crn cun wral wcel wa weq breq2 breq1 orbi12d rcla4va
      pm4.25 sylibr ralimiaa ) ADZBDZCEZUBUACEZFZBCGCHIZJZUAUACEZAUFUAUFKUGLUHU
      HFZUHUEUIBUAUFBAMUCUHUDUHUBUAUACNUBUAUACOPQUHRST $.
      $( [29-Dec-2011] $)
  $}


  $( Equality theorem for functions.  (Contributed by FL, 16-Nov-2008.) $)
  feq123 $p |- ( ( F = G /\ A = C /\ B = D ) ->
    ( F : A --> B <-> G : C --> D ) ) $=
    ( wceq w3a wf wb feq1 3ad2ant1 feq23 3adant1 bitrd ) EFGZACGZBDGZHABEIZABFI
    ZCDFIZPQSTJRABEFKLQRTUAJPABCDFMNO $.
    $( [16-Nov-2008] $)

  $( A class whose union is finite is finite.  (Contributed by FL,
     22-Dec-2008.) $)
  unfinsef $p |- ( U. A e. Fin -> A e. Fin ) $=
    ( cuni cfn wcel cpw pwfi wss pwuni ssfi mpan2 sylbi ) ABZCDLEZCDZACDZLFNAMG
    OAHMAIJK $.
    $( [22-Dec-2008] $)

  $( If the domain of a bijection is finite its range is finite and
     reciprocally.  (Contributed by FL, 31-Jul-2009.) $)
  f1ofi $p |- ( F : A -1-1-onto-> B -> ( A e. Fin <-> B e. Fin ) ) $=
    ( wf1o cfn wcel wfo f1ofo fofi ex syl5com ccnv wi f1ocnv expcom 3syl
    impbid ) ABCDZAEFZBEFZRABCGZSTABCHSUATABCIJKRBACLZDBAUBGZTSMABCNBAUBHTUCSBA
    UBIOPQ $.
    $( [31-Jul-2009] $)

  ${
    $d A B F x $.
    f1fi.1 $e |- F e. C $.
    $( If a 1-to-1 function has a finite codomain its domain is finite.
       (Contributed by FL, 31-Jul-2009.) $)
    f1fi $p |- ( ( B e. Fin /\ F : A -1-1-> B ) -> A e. Fin ) $=
      ( vx wf1 cfn wcel cv wf1o wss wa wex wi elisseti f11o ssfi f1ofi
      syl5ibrcom ex com13 imp 19.23aiv sylbi impcom ) ABDGZBHIZAHIZUGAFJZDKZUJB
      LZMZFNUHUIOZFABDDCEPQUMUNFUKULUNUHULUKUIUHULUKUIOUHULMUIUKUJHIBUJRAUJDSTU
      AUBUCUDUEUF $.
      $( [31-Jul-2009] $)
  $}

  $( Field of a square cross product.  (Contributed by FL, 10-Oct-2009.) $)
  fldsqcp2 $p |- U. U. ( A X. A ) = A $=
    ( c0 wceq cxp cuni xpeq1 xp0r syl6eq wi unieq unieqd uni0 unieqi eqtri wa
    eqtr expcom eqcoms syl5com sylancl mpcom wn wne df-ne xpnz cun unixp unidm
    sylbi sylancbr pm2.61i ) ABCZAADZEZEZACZUMBCZULUPULUMBADBABAFAGHUQUOBEZEZCZ
    USBCZULUPIUQUNURUMBJKUSURBURBLMLNUTVAOUOBCZULUPUOUSBPVBUPIBAVBBACUPUOBAPQRS
    TUAULUBABUCZVCUPABUDZVDVCVCOUMBUCZUPAAUEVEUOAAUFAAAUGAUHHUIUJUK $.
    $( [10-Oct-2009] $)

  ${
    $d A x y z $.
    $( A square cross product ` ( A X. A ) ` is a transitive relation.
       (Contributed by FL, 31-Jul-2009.) $)
    cpref $p |- ( ( A X. A ) o. ( A X. A ) ) C_ ( A X. A ) $=
      ( vx vy vz cxp ccom wss cv wbr wa wi wal wcel visset brxp biimpri expcom
      adantl sylbi com12 adantr imp ax-gen gen2 cotr mpbir ) AAEZUGFUGGBHZCHZUG
      IZUIDHZUGIZJUHUKUGIZKZDLZCLBLUOBCUNDUJULUMUJUHAMZUIAMZJULUMKZUHUIAACNOUPU
      RUQULUPUMULUQUKAMZJUPUMKZUIUKAADNZOUSUTUQUPUSUMUMUPUSJUHUKAAVAOPQRSTUASUB
      UCUDBCDUGUEUF $.
      $( [31-Jul-2009] $)
  $}

  $( A square cross product ` ( A X. A ) ` is reflexive.  (Contributed by FL,
     31-Jul-2009.) $)
  scprefat $p |- ( _I |` U. U. ( A X. A ) ) C_ ( A X. A ) $=
    ( cxp cuni wceq cid cres wss fldsqcp2 reseq2 wf1o f1oi wf f1of fssxp syl
    ax-mp sseq1 mpbiri ) AABZCCZADZETFZSGZAHUAUBEAFZDZUCTAEIUEUCUDSGZAAUDJZUFAK
    UGAAUDLUFAAUDMAAUDNOPUBUDSQROP $.
    $( [31-Jul-2009] $)

  $( Todo: combine scprefat and scprefat2. $)
  $( A square cross product ` ( A X. A ) ` is reflexive.  (Contributed by FL,
     31-Jul-2009.) $)
  scprefat2 $p |- ( _I |` A ) C_ ( A X. A ) $=
    ( cxp cuni wceq cid cres wss fldsqcp2 reseq2 scprefat sseq1 mpbiri eqcoms
    syl ax-mp ) AABZCCZADZEAFZPGZAHREQFZSDTQAEITSUASUADTUAPGAJSUAPKLMNO $.
    $( [31-Jul-2009] $)

  $( A square cross product is symmetric.  (Contributed by FL, 31-Jul-2009.) $)
  sqpsym $p |- `' ( A X. A ) C_ ( A X. A ) $=
    ( cxp ccnv cnvxp eqimssi ) AABZCFAADE $.
    $( [31-Jul-2009] $)

  $( A square cross product is an equivalence relation (in general it's not a
     poset).  (Contributed by FL, 31-Jul-2009.) $)
  sqpeq $p |- Er ( A X. A ) $=
    ( cxp wer ccnv ccom cun wss wceq cnvxp cpref uneq1 unss2 wi unidm wa eqtr
    sseq2 biimpd syl mpan2 syl2im mp2 df-er mpbir ) AABZCUEDZUEUEEZFZUEGZUFUEHZ
    UGUEGZUIAAIAJUJUFUEFZUEUEFZHZUKUHULGZUIUFUEUEKUGUEUFLUNUMUEHZUOUIMZUENUNUPO
    ULUEHZUQULUMUEPURUOUIULUEUHQRSTUAUBUEUCUD $.
    $( [31-Jul-2009] $)

  ${
    $d R x y z $.  $d S x y z $.
    $( The intersection of two transitive classes is transitive.  (Contributed
       by FL, 31-Jul-2009.) $)
    inttr $p |- ( ( ( R o. R ) C_ R /\ ( S o. S ) C_ S )
      -> ( ( R i^i S ) o. ( R i^i S ) ) C_ ( R i^i S ) ) $=
      ( vx vy vz ccom wss wa cv cin wbr wi wal cotr brin simpr simpl anim12d
      com12 an4s syl2anb syl6ibr alanimi ex sylbi imp sylibr ) AAFAGZBBFBGZHCIZ
      DIZABJZKZUKEIZULKZHZUJUNULKZLZEMZDMZCMZULULFULGUHUIVAUHUJUKAKZUKUNAKZHZUJ
      UNAKZLZEMZDMZCMZUIVALCDEANUIVIVAUIUJUKBKZUKUNBKZHZUJUNBKZLZEMZDMZCMZVIVAL
      CDEBNVQVIVAVPVHUTCVOVGUSDVNVFUREVNVFHZUPVEVMHZUQUPVRVSUMVBVJHVCVKHVRVSLZU
      OUJUKABOUKUNABOVBVCVJVKVTVRVDVLHVSVRVDVEVLVMVNVFPVNVFQRSTUASUJUNABOUBUCUC
      UCUDUESUEUFCDEULNUG $.
      $( [31-Jul-2009] $)
  $}

  $( The relation induced by a transitive relation on a part of its field is
     transitive.  (Taking the intersection of a relation with a square cross
     product is a way to restrict it to a subset of its field.)  (Contributed
     by FL, 31-Jul-2009.) $)
  tricptr $p |- ( ( R o. R ) C_ R ->
  ( ( R i^i ( A X. A ) ) o. ( R i^i ( A X. A ) ) ) C_ ( R i^i ( A X. A ) ) ) $=
    ( ccom wss cxp cin cpref inttr mpan2 ) BBCBDAAEZJCJDBJFZKCKDAGBJHI $.
    $( [31-Jul-2009] $)

  ${
    $d A x $.  $d B x $.
    $( If an infinite set ` A ` is included in the underlying set of a finite
       cover ` B ` then there exists a set of the cover that contains an
       infinite number of element of ` A ` .  (Contributed by FL,
       2-Aug-2009.) $)
    isunscov $p |- ( ( -. A e. Fin /\ B e. Fin /\ A C_ U. B ) ->
    E. x e. B -. ( A i^i x ) e. Fin ) $=
      ( cfn wcel wn cuni wss w3a cv cin wrex wral dfral2 wi wa ciun iunfi
      iunin2 eleq1i uniiun eqcomi ineq2i wceq df-ss eleq1 pm2.24 syl6bi sylbi
      com12 syl ex com24 3imp syl5bir pm2.18d ) BDEZFZCDEZBCGZHZIZBAJZKZDEZFACL
      ZVFFVEACMZVBVFVEACNURUSVAVGVFOZUSURVAVHOUSVGVAURVFUSVGVAURVFOZOZUSVGPACVD
      QZDEZVJACVDRVLBACVCQZKZDEZVJVKVNDACBVCSTVOBUTKZDEZVJVNVPDVMUTBUTVMACUAUBU
      CTVAVQVIVAVPBUDZVQVIOBUTUEVRVQUQVIVPBDUFUQVFUGUHUIUJUIUIUKULUMUJUNUOUP $.
      $( [2-Aug-2009] $)
  $}


  ${
    $d A f x $.
    $( ~ ac5 with the premisse transformed into an antecedent.  (Contributed by
       FL, 2-Aug-2009.) $)
    ac5g $p |- ( A e. _V ->
      E. f ( f Fn A /\ A. x e. A ( x =/= (/) -> ( f ` x ) e. x ) ) ) $=
      ( cvv wcel cv wfn c0 wne cfv wi wral wa wex cif wceq fneq2 raleq anbi12d
      exbidv 0ex elimel ac5 dedth ) BDEZCFZBGZAFZHIUHUFJUHEKZABLZMZCNUFUEBHOZGZ
      UIAULLZMZCNBHBULPZUKUOCUPUGUMUJUNBULUFQUIABULRSTAULCBHDUAUBUCUD $.
      $( [2-Aug-2009] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $d F x $.  $d H x $.  $d K x $.
    $( An injection is left-cancelable.  (Contributed by FL, 2-Aug-2009.) $)
    njtlc $p |- ( ( F : B -1-1-> C /\ H : A --> B /\ K : A --> B ) ->
      ( ( F o. H ) = ( F o. K ) -> H = K ) ) $=
      ( vx wf1 wf w3a wceq cv ccom cfv wral wa wcel wfun f1f ffun syl 3ad2ant1
      adantr simpl2 simpr fvco3 syl111anc simpl3 eqeq12d wi simpl1 ffvelrn
      3ad2antl2 3ad2antl3 f1fveq biimpd syl12anc sylbid ralimdva anim2d wfn wb
      ffn fnfco ex 3syl imp 3adant3 3adant2 eqfnfv syl11anc anim12i 3adant1
      3imtr4d ) BCDHZABEIZABFIZJZAAKZGLZDEMZNZVTDFMZNZKZGAOZPZVSVTENZVTFNZKZGAO
      ZPZWAWCKZEFKZVRWFWKVSVRWEWJGAVRVTAQZPZWEWHDNZWIDNZKZWJWPWBWQWDWRWPDRZVPWO
      WBWQKVRWTWOVOVPWTVQVOBCDIZWTBCDSZBCDTUAUBUCZVOVPVQWOUDVRWOUEZABVTDEUFUGWP
      WTVQWOWDWRKXCVOVPVQWOUHXDABVTDFUFUGUIWPVOWHBQZWIBQZWSWJUJVOVPVQWOUKVPVOWO
      XEVQABVTEULUMVQVOWOXFVPABVTFULUNVOXEXFPPWSWJBCWHWIDUOUPUQURUSUTVRWAAVAZWC
      AVAZWMWGVBVOVPXGVQVOVPXGVOXADBVAZVPXGUJXBBCDVCZXIVPXGBADEVDVEVFVGVHVOVQXH
      VPVOVQXHVOXAXIVQXHUJXBXJXIVQXHBADFVDVEVFVGVIGAAWAWCVJVKVREAVAZFAVAZPZWNWL
      VBVPVQXMVOVPXKVQXLABEVCABFVCVLVMGAAEFVJUAVN $.
      $( [2-Aug-2009] $)
  $}

  ${
    $d A y $.  $d B y $.  $d F x y $.  $d H x y $.  $d K x y $.
    $( A surjection is right-cancelable.  (Contributed by FL, 21-Nov-2011.) $)
    surrc2 $p |- ( ( F : A -onto-> B /\ H Fn B /\ K Fn B ) ->
      ( ( H o. F ) = ( K o. F ) <-> H = K ) ) $=
      ( vx vy wfo wfn w3a ccom wceq wa cdm cv cfv wral fndm eqtr3 syl2an
      3adant1 adantr wcel wi fveq1 wf wfun fof fnfun fvco3 3exp impcom 3adant3
      imp syl5com syl5 3adant2 eqeq12d biimpd ex com3r syl r19.21aiv wb foeq3
      eqcoms fveq2 cbvfo mpbid jca anim12i eqfunfv mpbird coeq1 impbid1 ) ABCHZ
      DBIZEBIZJZDCKZECKZLZDELZVSWBWCVSWBMZWCDNZENZLZFOZDPZWHEPZLZFWEQZMZWDWGWLV
      SWGWBVQVRWGVPVQWEBLZWFBLWGVRBDRZBERWEWFBSTUAUBWDGOZCPZDPZWQEPZLZGAQZWLWDW
      TGAWBVSWPAUCZWTUDZWBWPVTPZWPWAPZLZVSXCUDWPVTWAUEVSXBXFWTVSXBXFWTUDVSXBMZX
      FWTXGXDWRXEWSVSXBXDWRLZVPVQXBXHUDZVRVPABCUFZDUGZXIVQABCUHZBDUIZXKXJXIXKXJ
      XBXHABWPDCUJUKULTUMUNVSXBXEWSLZVPVRXBXNUDZVQVPVRXOVREUGZVPXOBEUIZVPXJXPXO
      XLXPXJXBXNABWPECUJUKUOUPUNUQUNURUSUTVAVBULVCWDAWECHZXAWLVDVSXRWBVPVQXRVRV
      QVPXRVQWNVPXRUDWOWNVPXRVPXRVDBWEBWEACVEVFUSVBULUMUBWTWKGFAWECWQWHLWRWIWSW
      JWQWHDVGWQWHEVGURVHVBVIVJWDXKXPMZWCWMVDVSXSWBVQVRXSVPVQXKVRXPXMXQVKUAUBFD
      EVLVBVMUTDECVNVO $.
      $( [21-Nov-2011] $)
  $}

  ${
    $d A x y $.
    $( Restriction of the identity to a singleton.  (Contributed by FL,
       2-Aug-2009.) $)
    restidsing $p |- ( _I |` { A } ) = ( { A } X. { A } ) $=
      ( vx vy cid csn cres cxp relres relxp cv cop wcel wa wbr df-br bicomi
      anbi1i simpr wceq elsn wi cvv visset ideqg biimpd ax-mp eqtr2 ex syl6ibr
      syl syl5bi imp jca eqtr3 ideq equcom bitri sylibr sylbi com12 simpl
      impbii opelres opelxp 3bitr4i eqrelriiv ) BCDAEZFZVGVGGZDVGHVGVGIBJZCJZKZ
      DLZVJVGLZMZVNVKVGLZMZVLVHLVLVILVOVJVKDNZVNMZVQVMVRVNVRVMVJVKDOPQVSVQVSVNV
      PVRVNRVRVNVPVNVJASZVRVPBATZVRVJVKSZVTVPUAVKUBLZVRWBUACUCZWCVRWBVJVKUBUDUE
      UFWBVTVKASZVPWBVTWEVJVKAUGUHCATZUIUJUKULUMVQVRVNVNVPVRVNVTVPVRUAWAVPVTVRV
      PWEVTVRUAWFWEVTVRWEVTMVKVJSZVRVKVJAUNVRWBWGVJVKWDUOBCUPUQURUHUSUTUSULVNVP
      VAUMVBUQVJVKDVGWDVCVJVKVGVGWDVDVEVF $.
      $( [2-Aug-2009] $)
  $}

  $( The intersection of the identity function with a square cross product.
     (Contributed by FL, 2-Aug-2009.) $)
  residcp $p |- ( _I i^i ( A X. A ) ) = ( _I |` A ) $=
    ( cid cxp cin cres cvv wss ssid ssv xpss12 mp2an sslin ax-mp df-res
    sseqtr4i resss scprefat2 ssini eqssi ) BAACZDZBAEZUABAFCZDZUBTUCGZUAUDGAAGA
    FGUEAHAIAAAFJKTUCBLMBANOUBBTBAPAQRS $.
    $( [2-Aug-2009] $)

  $( Image under a restricted class.  (Contributed by FL, 31-Aug-2009.) $)
  imrescl $p |- ( A C_ B -> ( ( C |` B ) " A ) = ( C " A ) ) $=
    ( wss cres cima crn cin wceq df-ima a1i resres rneqi df-ss incom reseq2d
    rneqd reseq2 eqcomi 3eqtrd sylbi ) ABDZCBEZAFZUCAEZGZCBAHZEZGZCAFZUDUFIUBUC
    AJKUFUIIUBUEUHCBALMKUBABHZAIZUIUJIABNULUICUKEZGCAEZGZUJULUHUMULUGUKCUGUKIUL
    BAOKPQULUMUNUKACRQUOUJIULUJUOCAJSKTUAT $.
    $( [31-Aug-2009] $)

  ${
    $d R x y $.
    $( Two ways of saying a relation is symmetric.  (Contributed by FL,
       31-Aug-2009.) $)
    twsymr $p |- ( Rel R -> ( R = `' R <-> A. x A. y ( x R y -> y R x ) ) ) $=
      ( ccnv wceq wss wa wrel cv wbr wi wal eqss cnvsym biimpi a1d adantl
      com12 dfrel2 cnvss sseq1 syl5ibcom sylbir sylbi imp biimpri jca ex
      impbid syl5bb ) CCDZECUKFZUKCFZGZCHZAIZBIZCJUQUPCJKBLALZCUKMUOUNURUNUOURU
      MUOURKULUMURUOUMURABCNZOPQRUOURUNUOURGULUMUOURULUOUKDZCEZURULKCSURVAULURU
      MVAULKUSUMUTUKFVAULUKCTUTCUKUAUBUCRUDUEURUMUOUMURUSUFQUGUHUIUJ $.
      $( [31-Aug-2009] $)
  $}

  ${
    $d R x y z $.
    $( Projection of the first elements of the pairs of a relation ` R ` .
       (Contributed by FL, 15-Oct-2012.) $)
    prj1b $p |- ( Rel R -> ( 1st " R ) =
    { x | E. y <. x , y >. e. R } ) $=
      ( vz wrel cv c1st cima wcel cop wex cab wb wal wceq wbr wrex wi c2nd cfv
      cvv cxp wss df-rel ssel wa elxp6 df-br wfn wfo fo1st fofn ax-mp visset
      fnopfvb mp2an eleq1 opeq1 eleq1d biimpcd syl6bi com3r sylbir sylbi com3l
      adantr syli com23 com13 fvex opeq2 cla4ev syl8 r19.23aiv com12 op1st
      opex mpbi mpbir breq1 rcla4ev mpan2 19.23aiv impbid1 elimag abid 3bitr4g
      19.21aiv ax-17 hbab1 cleqf sylibr ) CEZAFZGCHZIZWNWNBFZJZCIZBKZALZIZMZANW
      OXAOWMXCAWMDFZWNGPZDCQZWTWPXBWMXFWTXFWMWTXEWMWTRDCXDCIZXEWMWNXDSTZJZCIZWT
      WMXEXGXJWMCUAUAUBZUCZXEXGXJRZRCUDXLXGXEXJXGXLXDXKIZXEXJRZCXKXDUEXNXDXDGTZ
      XHJZOZXPUAIXHUAIUFZUFXGXORZXDUAUAUGXRXTXSXEXRXGXJXEXDWNJGIZXRXMRZXDWNGUHY
      AXPWNOZYBGUAUIZXDUAIYCYAMUAUAGUJYDUKUAUAGULUMZDUNUAXDWNGAUNZUOUPXRXGYCXJX
      RXGXQCIZYCXJRXDXQCUQYCYGXJYCXQXICXPWNXHURUSUTVAVBVCVDVEVFVDVGVHVDVIWSXJBX
      HXDSVJWQXHOWRXICWQXHWNVKUSVLVMVNVOWSXFBWSWRWNGPZXFYHWRWNJGIZWRGTWNOZYIWNW
      QYFVPYDWRUAIYJYIMYEWNWQVQUAWRWNGYFUOUPVRWRWNGUHVSXEYHDWRCXDWRWNGVTWAWBWCW
      DWNUAIWPXFMYFDWNGCUAWEUMWTAWFWGWHADWOXAXDWOIAWIWTADWJWKWL $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d R x y z $.
    $( Use fnopfvb instead of fnotoprb. $)
    $( Projection of the second elements of the pairs of a relation ` R ` .
       (Contributed by FL, 15-Oct-2012.) $)
    prj3 $p |- ( Rel R -> ( 2nd " R ) = { y | E. x <. x , y >. e. R } ) $=
      ( vz wrel cv c2nd cima wcel cop wex cab wb wal wceq wbr wrex wi c1st cfv
      cvv cxp wss df-rel ssel wa elxp6 df-br wfn wfo fo2nd fofn ax-mp visset
      fnopfvb mp2an eleq1 opeq2 eleq1d biimpcd syl6bi com3r sylbir sylbi com3l
      adantr syli com23 com13 fvex opeq1 cla4ev syl8 r19.23aiv com12 op2nd
      opex mpbi mpbir breq1 rcla4ev mpan2 19.23aiv impbid1 elimag abid 3bitr4g
      19.21aiv ax-17 hbab1 cleqf sylibr ) CEZBFZGCHZIZWNAFZWNJZCIZAKZBLZIZMZBNW
      OXAOWMXCBWMDFZWNGPZDCQZWTWPXBWMXFWTXFWMWTXEWMWTRDCXDCIZXEWMXDSTZWNJZCIZWT
      WMXEXGXJWMCUAUAUBZUCZXEXGXJRZRCUDXLXGXEXJXGXLXDXKIZXEXJRZCXKXDUEXNXDXHXDG
      TZJZOZXHUAIXPUAIUFZUFXGXORZXDUAUAUGXRXTXSXEXRXGXJXEXDWNJGIZXRXMRZXDWNGUHY
      AXPWNOZYBGUAUIZXDUAIYCYAMUAUAGUJYDUKUAUAGULUMZDUNUAXDWNGBUNZUOUPXRXGYCXJX
      RXGXQCIZYCXJRXDXQCUQYCYGXJYCXQXICXPWNXHURUSUTVAVBVCVDVEVFVDVGVHVDVIWSXJAX
      HXDSVJWQXHOWRXICWQXHWNVKUSVLVMVNVOWSXFAWSWRWNGPZXFYHWRWNJGIZWRGTWNOZYIWQW
      NAUNYFVPYDWRUAIYJYIMYEWQWNVQUAWRWNGYFUOUPVRWRWNGUHVSXEYHDWRCXDWRWNGVTWAWB
      WCWDWNUAIWPXFMYFDWNGCUAWEUMWTBWFWGWHBDWOXAXDWOIBWIWTBDWJWKWL $.
      $( [15-Oct-2012] $)
  $}


  ${
    $d A x $.
    $( The image under ` 1st ` of a class with no pairs inside.  (Contributed
       by FL, 31-Aug-2009.) $)
    imfstnrelc $p |- ( ( ( A i^i ( _V X. _V ) ) = (/) /\ A =/= (/) )
   -> ( 1st " A ) = { (/) } ) $=
      ( vx cvv cxp cin c0 wceq wne wa c1st cima cv cfv csn ciun wfun cdm wss
      wfo fo1st fofun ax-mp ssv wfn fofn fndm sseqtr4i pm3.2i a1i dfimafn2 syl
      wcel cuni 1stval wn wi wral wal disj biimpi df-ral sylib 19.21bi adantr
      imp dmsnn0 bicomi necon1bbii 0ss sseq1 mpbiri sylbi uni0b sylibr syl5eq
      sneqd iuneq2dv iunconst adantl 3eqtrd ) ACCDZEFGZAFHZIZJAKZBABLZJMZNZOZBA
      FNZOZWJWDJPZAJQZRZIZWEWIGWOWDWLWNCCJSZWLTCCJUAUBACWMAUCJCUDZWMCGWPWQTCCJU
      EUBCJUFUBUGUHUIBAJUJUKWDBAWHWJWDWFAULZIZWGFWSWGWFNQZUMZFWFUNWSWTWJRZXAFGW
      SWFWAULZUOZXBWDWRXDWBWRXDUPZWCWBXEBWBXDBAUQZXEBURWBXFBAWAUSUTXDBAVAVBVCVD
      VEXDWTFGZXBXCWTFXCWTFHWFVFVGVHXGXBFWJRWJVIWTFWJVJVKVLUKWTVMVNVOVPVQWCWKWJ
      GWBBAWJVRVSVT $.
      $( [31-Aug-2009] $)
  $}

  ${
    $d R x y $.
    $( The projection of the first elements of the pairs of a relation ` R ` is
       its domain.  (Contributed by FL, 5-Oct-2009.) $)
    prjdmn $p |- ( Rel R -> ( 1st " R ) = dom R ) $=
      ( vx vy wrel c1st cima cv cop wcel wex cab cdm prj1b dfdm3 syl6eqr ) ADEA
      FBGCGHAICJBKALBCAMBCANO $.
      $( [5-Oct-2009] $)
  $}

  ${
    $d R x y $.
    $( The projection of the second elements of the pairs of a relation ` R `
       is its range.  (Contributed by FL, 15-Oct-2012.) $)
    prjrn $p |- ( Rel R -> ( 2nd " R ) = ran R ) $=
      ( vx vy wrel c2nd cima cv cop wcel wex cab crn prj3 dfrn3 syl6eqr ) ADEAF
      BGCGHAIBJCKALBCAMBCANO $.
      $( [15-Oct-2012] $)
  $}

  $( Projection of a cross product.  (Contributed by FL, 5-Oct-2009.) $)
  prjcp1 $p |- ( B =/= (/) -> ( 1st " ( A X. B ) ) = A ) $=
    ( c0 wne c1st cxp cima cdm wrel wceq wi relxp prjdmn a1d ax-mp dmxp eqtrd
    ) BCDZEABFZGZSHZASIZRTUAJZKABLUBUCRSMNOABPQ $.
    $( [5-Oct-2009] $)

  $( Projection of a cross product.  (Contributed by FL, 15-Oct-2012.) $)
  prjcp2 $p |- ( A =/= (/) -> ( 2nd " ( A X. B ) ) = B ) $=
    ( c0 wne c2nd cxp cima crn wrel wceq wi relxp prjrn a1d ax-mp rnxp eqtrd )
    ACDZEABFZGZSHZBSIZRTUAJZKABLUBUCRSMNOABPQ $.
    $( [15-Oct-2012] $)

  ${
    $d A v w x y z $.  $d ph x $.  $d ta v w y z $.
    eloi.1 $e |- ( y = ( 1st ` ( 1st ` A ) ) -> ( ph <-> ps ) ) $.
    eloi.2 $e |- ( z = ( 2nd ` ( 1st ` A ) ) -> ( ps <-> ch ) ) $.
    eloi.3 $e |- ( v = ( 1st ` ( 2nd ` A ) ) -> ( ch <-> th ) ) $.
    eloi.4 $e |- ( w = ( 2nd ` ( 2nd ` A ) ) -> ( th <-> ta ) ) $.
    $( A consequence of membership in a class abstraction whose elements belong
       to ` ( ( _V X. _V ) X. ( _V X. _V ) ) ` using ordered pair extractors.
       (Used by category theory).  (Contributed by FL, 24-Sep-2007.) $)
    eloi $p |- ( A e. { x | E. y E. z E. v E. w
      ( x = <. <. y , z >. , <. v , w >. >. /\ ph ) } -> ta ) $=
      ( cv cop wceq wa wex cab wcel c1st cfv c2nd eqeq1 anbi1d 4exbidv elabg
      ibi cvv cxp id opex opelxpi mp2an syl6eqel wrel relxp 1st2nd mpan syl
      fveq2 op1st syl6eq visset op2nd opeq12d eqtrd adantr 19.23aivv eleq1 w3a
      wb fvex 3pm3.2i elo syl6bb biimpcd mpd ) KFPZGPZHPZQZJPZIPZQZQZRZASZITJTH
      TGTZFUAZUBZKKUCUDZUCUDZWNUEUDZQZKUEUDZUCUDZWRUEUDZQZQZRZEWMKWHRZASZITJTZH
      TGTZXCWMXGWKXGFKWLWAKRZWJXEGHJIXHWIXDAWAKWHUFUGUHUIUJXFXCGHXEXCJIXDXCAXDK
      WNWRQZXBXDKUKUKULZUBZKXIRZXDKWHXJXDUMWDUKUBWGUKUBWHXJUBWBWCUNZWEWFUNZWDWG
      UKUKUOUPUQXJURZXKXLUKUKUSZKXJUTVAVBXDWNWQWRXAXDWNXJUBZWNWQRZXDWNWDXJXDWNW
      HUCUDWDKWHUCVCWDWGXMVDVEWBUKUBWCUKUBWDXJUBGVFHVFWBWCUKUKUOUPUQXOXQXRXPWNX
      JUTVAVBXDWRXJUBZWRXARZXDWRWGXJXDWRWHUEUDWGKWHUEVCWDWGXMXNVGVEWEUKUBWFUKUB
      WGXJUBJVFIVFWEWFUKUKUOUPUQXOXSXTXPWRXJUTVAVBVHVIVJVKVKVBXCWMEXCWMXBWLUBZE
      KXBWLVLWOUKUBZWPUKUBZWSUKUBZVMWTUKUBYAEVNYBYCYDWNUCVOWNUEVOWRUCVOVPWRUEVO
      ABCDEFGHIJWOWPWSWTUKUKUKUKLMNOVQUPVRVSVT $.
      $( [24-Sep-2007] $)
  $}

  $c t+ $.
  $c t* $.

  $( Extend class notation to include the transitive closure symbol. $)
  ctcl $a class t+ $.

  $( Extend class notation with transitive closure. $)
  crtcl $a class t* $. $( Extend class notation with reflexive transitive
    closure. $)

  ${
    $d x y z $.
    $( Transitive closure of a relation.  Experimental. $)
    df-trcl $a |- t+ = { <. x , y >. |
      y = |^| { z | ( x C_ z /\ ( z o. z ) C_ z ) } } $.

    $( Reflexive-transitive closure of a relation.  Experimental. $)
    df-rtrcl $a |- t* = { <. x , y >. |
      y = |^| { z | ( ( _I |` ( dom x u. ran x ) ) C_ z /\ x C_ z
      /\ ( z o. z ) C_ z ) } } $.
  $}


  $( The double union of an intersection is a part of the intersections of the
     unions.  (Contributed by FL, 24-Jan-2010.) $)
  uuniin $p |- U. U. ( A i^i B ) C_ ( U. U. A i^i U. U. B ) $=
    ( cin cuni wss uniin uniss sstr2 mpi syl ax-mp ) ABCDZADZBDZCZEZLDZMDNDCZEZ
    ABFPQODZEZSLOGUATRESMNFQTRHIJK $.
    $( [24-Jan-2010] $)


  $( Building a set with two elements.  (Contributed by FL, 11-Aug-2008.) $)
  set2elt $p |- ( ( C ~~ 2o /\ A e. C /\ B e. C )
     -> ( A =/= B -> C = { A , B } ) ) $=
    ( c2o cen wbr wcel wne cpr wceq wi wa wss prssg w3a pr2nelem cfn com 2onn
    ssid ssnnfi mp2an wb enfi mpan mpbiri ensymg ax-mp entr prex ensym
    fisseneq eqcomd 3exp com3r syl ex syl5com com13 mpcom 3expia com24 sylbid
    pm2.43i com12 3impib ) CDEFZACGZBCGZABHZCABIZJZKZVHVILZVGVMVNVGVMKZVNVNVKCM
    ZVOABCCCNVNVJVGVPVLVHVIVJVGVPVLKZKVHVIVJOVKDEFZVGVQABCCPCQGZVGVRVQKVGVSDQGZ
    DRGZDDMVTSDTDDUAUBWAVGVSVTUCSCDRUDUEUFVRVGVSVQVRDVKEFZVGVSVQKZWAVRWBKSVKDRU
    GUHVGWBWCVGWBLCVKEFZWCCDVKUIWDVKCEFZWCCVKABUJUKVSVPWEVLVSVPWEVLVSVPWEOVKCVK
    CULUMUNUOUPUPUQURUSUTURVAVBVCVDVEVF $.
    $( [11-Aug-2008] $)

  $( The range of a square cross product.  (Contributed by FL, 17-May-2010.) $)
  rnxpid $p |- ran ( A X. A ) = A $=
    ( cxp crn wceq c0 rn0 xpeq2 xp0 syl6eq rneqd id 3eqtr4a rnxp pm2.61ine ) AA
    BZCZADAEAEDZECEPAFQOEQOAEBEAEAGAHIJQKLAAMN $.
    $( [17-May-2010] $)

  ${
    opreq123i.1 $e |- A = C $.
    opreq123i.2 $e |- B = D $.
    opreq123i.3 $e |- F = G $.
    $( Equality inference for operation value.  (Contributed by FL,
       11-Jul-2010.) $)
    opreq123i $p |- ( A F B ) = ( C G D ) $=
      ( co opreq12i opreqi eqtri ) ABEJCDEJCDFJACBDEGHKEFCDILM $.
      $( [11-Jul-2010] $)
  $}

  $( A class builder defined by a negation.  (Contributed by FL,
     18-Sep-2010.) $)
  clsbldneg $p |- { x | -. ph } = ( _V \ { x | ph } ) $=
    ( wn cvv crab cv wcel wa cab cdif df-rab rabab difab abid2 difeq1i eqtr3i
    3eqtr3i ) ACZBDEBFDGZRHBIZRBIDABIZJZRBDKRBLSBIZUAJTUBSABMUCDUABDNOPQ $.
    $( [18-Sep-2010] $)


  $( A class builder defined by an implication.  (Contributed by FL,
     18-Sep-2010.) $)
  clsbldimp $p |- { x | ( ph -> ps ) } =
                  ( { x | -. ph } u. { x | ps } ) $=
    ( wi cab wn wo cun imor abbii unab eqtr4i ) ABDZCEAFZBGZCENCEBCEHMOCABIJNBC
    KL $.
    $( [18-Sep-2010] $)


  $( A mapping is a member of the powerset of the cross product of its domain
     and codomain.  (Contributed by FL, 30-Dec-2010.) $)
  mappow $p |- ( ( A e. R /\ B e. S ) ->
    ( F : A --> B -> F e. ~P ( A X. B ) ) ) $=
    ( wcel wa wf cmap co cxp cpw wb elmapg ancoms wss mapsspw adantr sseld
    sylbird ) ACFZBDFZGZABEHZEBAIJZFZEABKLZFUBUAUFUDMBAEDCNOUCUEUGEUAUEUGPUBBAC
    QRST $.
    $( [30-Dec-2010] $)

  ${
    $d A x y $.  $d ph y $.
    $( Membership in the intersection of a class abstraction.  (Contributed by
       FL, 30-Dec-2010.) $)
    elintabg $p |- ( A e. V ->
      ( A e. |^| { x | ph } <-> A. x ( ph -> A e. x ) ) ) $=
      ( vy cv cab cint wcel wi wal wb wceq eleq1 imbi2d albidv bibi12d visset
      elintab vtoclg ) EFZABGHZIZAUABFZIZJZBKZLCUBIZACUDIZJZBKZLECDUACMZUCUHUGU
      KUACUBNULUFUJBULUEUIAUACUDNOPQABUAERST $.
      $( [30-Dec-2010] $)
  $}

  $( A singleton of a set belongs to the power class of a class containing the
     set.  (Contributed by FL, 30-Dec-2010.) $)
  snelpwg $p |- ( A e. V -> ( A e. B <-> { A } e. ~P B ) ) $=
    ( wcel cvv csn cpw wb elisset c0 cif wceq eleq1 sneq eleq1d bibi12d 0ex
    elimel snelpw dedth syl ) ACDAEDZABDZAFZBGZDZHZACIUBUGUBAJKZBDZUHFZUEDZHAJA
    UHLZUCUIUFUKAUHBMULUDUJUEAUHNOPUHBAJEQRSTUA $.
    $( [30-Dec-2010] $)

  ${
    $d A x y $.  $d F z $.  $d x y z $.
    dff1o6f.1 $e |- ( z e. F -> A. x z e. F ) $.
    dff1o6f.2 $e |- ( z e. F -> A. y z e. F ) $.
    $( A one-to-one onto function in terms of function values.  (Contributed by
       FL, 1-Jan-2011.) $)
    dff1o6f $p |- ( F : A -1-1-onto-> B <-> ( F Fn A /\ ran F = B /\
             A. x e. A A. y e. A ( ( F ` x ) = ( F ` y ) -> x = y ) ) ) $=
      ( wf1o wf1 wfo wa wf cv cfv wceq wi wral wfn crn w3a df-f1o dff13f df-fo
      anbi12i df-3an wss eqimss anim2i df-f sylibr pm4.71ri anbi1i an32
      3bitrri 3bitri ) DEFIDEFJZDEFKZLDEFMZANZFOBNZFOPUTVAPQBDRADRZLZFDSZFTZEPZ
      LZLZVDVFVBUAZDEFUBUQVCURVGABCDEFGHUCDEFUDUEVIVGVBLUSVGLZVBLVHVDVFVBUFVGVJ
      VBVGUSVGVDVEEUGZLUSVFVKVDVEEUHUIDEFUJUKULUMUSVGVBUNUOUP $.
      $( [1-Jan-2011] $)
  $}

  $( The non-empty components of a finite cross product are finite.
     (Contributed by FL, 22-Feb-2011.) $)
  fixpb $p |- ( ( A =/= (/) /\ B =/= (/) ) ->
    ( ( A X. B ) e. Fin <-> ( A e. Fin /\ B e. Fin ) ) ) $=
    ( c0 wne wa cxp cfn wcel wi xpnz xpfir expcom sylbi xpfi impbid1 ) ACDBCDEZ
    ABFZGHZAGHBGHEZPQCDZRSIABJRTSABKLMABNO $.
    $( [22-Feb-2011] $)

  $( A cross product is finite iff one of its components is empty or both its
     components are finite.  (Contributed by FL, 22-Feb-2011.) $)
  fixpc $p |- ( ( A X. B ) e. Fin <->
       ( A = (/) \/ B = (/) \/ ( A e. Fin /\ B e. Fin ) ) ) $=
    ( cxp cfn wcel c0 wceq wa w3o wo wn wne wi neanior fixpb biimpd sylbir
    com12 orrd df-3or sylibr xpeq1 xp0r 0fin eqeltri syl6eqel xpeq2 xp0 xpfi
    3jaoi impbii ) ABCZDEZAFGZBFGZADEBDEHZIZUMUNUOJZUPJUQUMURUPURKZUMUPUSAFLBFL
    HZUMUPMAFBFNUTUMUPABOPQRSUNUOUPTUAUNUMUOUPUNULFBCZDAFBUBVAFDBUCUDUEUFUOULAF
    CZDBFAUGVBFDAUHUDUEUFABUIUJUK $.
    $( [22-Feb-2011] $)

  $( The power set of a set is equinumerous to set exponentiation with a base
     of ordinal ` 2o ` .  (Contributed by FL, 22-Feb-2011.) $)
  pw2eng $p |- ( A e. V -> ~P A ~~ ( 2o ^m A ) ) $=
    ( wcel cvv cpw c2o cmap co cen wbr elisset c0 cif wceq pweq opreq2 breq12d
    0ex elimel pw2en dedth syl ) ABCADCZAEZFAGHZIJZABKUCUFUCALMZEZFUGGHZIJALAUG
    NUDUHUEUIIAUGOAUGFGPQUGALDRSTUAUB $.
    $( [22-Feb-2011] $)

  ${
    $d x y $.
    $( The class of natural numbers is the intersection of the limits
       ordinals.  (Contributed by FL, 22-Feb-2011.) $)
    omslim $p |- om = ( On i^i |^| { x | Lim x } ) $=
      ( vy com con0 cv wlim cab cint cin word wcel wi wal wa visset elon
      bicomi elintab anbi12i elom elin 3bitr4i eqriv ) BCDAEZFZAGHZIZBEZJZUEUHU
      DKLAMZNUHDKZUHUFKZNUHCKUHUGKUIUKUJULUKUIUHBOZPQULUJUEAUHUMRQSAUHUMTUHDUFU
      AUBUC $.
      $( [22-Feb-2011] $)

    $( Assuming the axiom of infinity, the class of natural numbers is the
       intersection of all the limit ordinals.  Since ` om ` is a limit ordinal
       it is then the smallest limit ordinal.  (Contributed by FL,
       22-Feb-2011.) $)
    omslim2 $p |- om = |^| { x | Lim x } $=
      ( vy com cv wlim cab cint wcel wi wal abid dfom4 eleq2i visset elintab
      3bitr4i eqriv ) BCADZEZAFGZBDZSUARHIAJZBFZHUBUACHUATHUBBKCUCUABALMSAUABNO
      PQ $.
      $( [22-Feb-2011] $)
  $}

  $( Absorption law for multiplication with an infinite cardinal.  Equivalent
     to Proposition 10.41 of [TakeutiZaring] p. 95.  (Contributed by FL,
     22-Feb-2011.) $)
  infxpg $p |- ( ( om ~<_ A /\ B =/= (/) /\ ( A e. C /\ B e. D ) ) ->
                   ( A X. B ) ~~ ( A u. B ) ) $=
    ( com cdom wbr c0 wne wcel wa cxp cun cen cvv wi elisset cif wceq breq2
    xpeq1 uneq1 breq12d imbi2d imbi12d neeq1 xpeq2 uneq2 omex elimel infxp ex
    dedth2h syl2an com3l 3imp ) EAFGZBHIZACJZBDJZKZABLZABMZNGZVAUQURVDUSAOJZBOJ
    ZUQURVDPZPZUTACQBDQVEVFVHEVEAERZFGZURVIBLZVIBMZNGZPZPVJVFBERZHIZVIVOLZVIVOM
    ZNGZPZPABEEAVISZUQVJVGVNAVIEFTWAVDVMURWAVBVKVCVLNAVIBUAAVIBUBUCUDUEBVOSZVNV
    TVJWBURVPVMVSBVOHUFWBVKVQVLVRNBVOVIUGBVOVIUHUCUEUDVJVPVSVIVOAEOUIUJBEOUIUJU
    KULUMUNUOUP $.
    $( [22-Feb-2011] $)

  $( An infinite set strictly dominates a natural number.  (Contributed by FL,
     22-Feb-2011.) $)
  infsdomnng $p |- ( A e. V -> ( ( om ~<_ A /\ B e. om ) -> B ~< A ) ) $=
    ( wcel cvv com cdom wbr wa csdm wi elisset cif wceq breq2 anbi1d imbi12d
    omex elimel infsdomnn dedth syl ) ACDAEDZFAGHZBFDZIZBAJHZKZACLUCUHFUCAFMZGH
    ZUEIZBUIJHZKAFAUINZUFUKUGULUMUDUJUEAUIFGOPAUIBJOQUIBAFERSTUAUB $.
    $( [22-Feb-2011] $)

  $( Any operation can be restricted to ` ( _V X. _V ) ` .  (Contributed by FL,
     20-Mar-2011.) $)
  resid2 $p |- ( ( Rel A /\ Rel dom A ) -> ( A |` ( _V X. _V ) ) = A ) $=
    ( cdm wrel cvv cxp wss cres wceq df-rel relssres sylan2b ) ABZCACLDDEZFAMGA
    HLIAMJK $.
    $( [20-Mar-2011] $)

  $( Image of an element of transitive class ` B ` under a class restricted by
     ` B ` .  (Contributed by FL, 20-Mar-2011.) $)
  imrestr $p |- ( ( Tr B /\ A e. B ) -> ( ( C |` B ) " A ) = ( C " A ) ) $=
    ( wtr wcel cres cima wceq wss trss imrescl syl6 imp ) BDZABEZCBFAGCAGHZNOAB
    IPBAJABCKLM $.
    $( [20-Mar-2011] $)

  $( Image of an element of a ordinal ` B ` under a class restricted by
     ` B ` .  (Contributed by FL, 20-Mar-2011.) $)
  imresord $p |- ( ( Ord B /\ A e. B ) -> ( ( C |` B ) " A ) = ( C " A ) ) $=
    ( word wtr wcel cres cima wceq ordtr imrestr sylan ) BDBEABFCBGAHCAHIBJABCK
    L $.
    $( [20-Mar-2011] $)

  $( If ` A ` is a part of ` B ` and ` B ` a part of ` C ` and ` A ` is
     equipotent to ` C ` then ` A ` is equipotent to ` B ` .  The art of
     sandwich applied to set theory.  (Contributed by FL, 16-Apr-2011.) $)
  sndw $p |- ( ( A C_ B /\ B C_ C /\ C e. _V )
     -> ( A ~~ C -> A ~~ B ) ) $=
    ( wss cvv wcel w3a cen wbr cdom wa wi relen brrelexi 3ad2ant3 ssdomg syl
    3exp com4r 3imp1 ssdom2g impcom 3adant1 adantr ensymg endom syl6 imp domtr
    syl11anc jca ex sbth ) ABDZBCDZCEFZGZACHIZABJIZBAJIZKZABHIUQURVAUQURKZUSUTU
    NUOUPURUSUOUPURUNUSUOUPURUNUSLZUOUPURGAEFZVCURUOVDUPACHMNOABEPQRSTVBBCJIZCA
    JIZUTUQVEURUOUPVEUNUPUOVEBCEUAUBUCUDUQURVFUQURCAHIZVFUPUNURVGLUOACEUEOCAUFU
    GUHBCAUIUJUKULABUMUG $.
    $( [16-Apr-2011] $)

  $( If ` A ` is a part of ` B ` and ` B ` a part of ` C ` and ` A ` is
     equipotent to ` C ` then ` A ` is equipotent to ` B ` .  The art of
     sandwich applied to set theory.  (Contributed by FL, 16-Apr-2011.) $)
  sndw2 $p |- ( ( A C_ B /\ B C_ C /\ C e. D )
     -> ( A ~~ C -> A ~~ B ) ) $=
    ( wcel wss cvv cen wbr wi elisset sndw syl3an3 ) CDEABFBCFCGEACHIABHIJCDKAB
    CLM $.
    $( [16-Apr-2011] $)

  $( If a successor of ` A ` belongs to an ordinal, so does ` A ` .
     (Contributed by FL, 20-Mar-2011.) $)
  ordsuccl $p |- ( ( Ord B /\ suc A e. B ) -> A e. B ) $=
    ( word wtr csuc wcel ordtr trsuc sylan ) BCBDAEBFABFBGBAHI $.
    $( [20-Mar-2011] $)

  $( If a successor of ` A ` belongs to an ordinal, so does ` A ` .
     (Contributed by FL, 20-Mar-2011.) $)
  ordsuccl2 $p |- ( ( B e. On /\ suc A e. B ) -> A e. B ) $=
    ( con0 wcel wtr csuc word eloni ordtr syl trsuc sylan ) BCDZBEZAFBDABDMBGNB
    HBIJBAKL $.
    $( [20-Mar-2011] $)

  $( If a successor of ` A ` belongs to an ordinal, ` A ` is a part of the
     ordinal.  (Contributed by FL, 20-Mar-2011.) $)
  ordsuccl3 $p |- ( ( B e. On /\ suc A e. B ) -> A C_ B ) $=
    ( con0 wcel csuc wa wtr wss word eloni ordtr syl adantr ordsuccl2 trss
    sylc ) BCDZAEBDZFBGZABDABHQSRQBISBJBKLMABNBAOP $.
    $( [20-Mar-2011] $)

  $( Trichotomy law for dominance and strict dominance.  This theorem is
     equivalent to the Axiom of Choice.  (Contributed by FL, 16-Apr-2011.) $)
  domtri2 $p |- ( ( A e. C /\ B e. D ) -> ( -. A ~<_ B <-> B ~< A ) ) $=
    ( wcel wa csdm wbr cdom wn domtri bicomd con1bid ) ACEBDEFZBAGHZABIHZNPOJAB
    CDKLM $.
    $( [16-Apr-2011] $)

  $( Omega strictly dominates a finite set.  (Contributed by FL,
     16-Apr-2011.) $)
  isfinite1b $p |- ( A e. Fin -> A ~< om ) $=
    ( cfn wcel com cdom wbr cen wn wa csdm isfinite1 omex ensym con3i anim2i
    brsdom sylibr syl ) ABCADEFZDAGFZHZIZADJFZAKUBSADGFZHZIUCUAUESUDTADLMNOADPQ
    R $.
    $( [16-Apr-2011] $)

  $( The cross product of two finite sets is finite.  (Contributed by FL,
     16-Apr-2011.) $)
  cptwff $p |- ( ( A ~< om /\ B ~< om ) -> ( A X. B ) ~< om ) $=
    ( com csdm wbr wa cxp cfn wcel isfinite3 xpfi syl2anbr sylib ) ACDEZBCDEZFA
    BGZHIZPCDENAHIBHIQOAJBJABKLPJM $.
    $( [16-Apr-2011] $)

  $( The intersection of a non-empty element of a transitive class is a part of
     the class.  (Contributed by FL, 15-Apr-2011.) $)
  inttrp $p |- ( ( Tr A /\ B e. A /\ B =/= (/) ) -> |^| B C_ A ) $=
    ( wtr wcel c0 wne w3a cint wss wi wa trss imp cuni uniss df-tr sstr
    intssuni sstr2 syl 3ad2ant3 syl5com ex com3l sylbi 3ad2ant1 pm2.43i
    3adant3 ) ACZBADZBEFZGZBHZAIZUIUJULUNJZUKUIUJKBAIZUOUIUJUPABLMUPBNZANZIZULU
    NBAOULUSUNJZUIUJULUTJZUKUIURAIZVAAPUSVBULUNUSVBUOUSVBKUQAIZULUNUQURAQUKUIVC
    UNJZUJUKUMUQIVDBRUMUQASTUAUBUCUDUEUFUGUBTUHUG $.
    $( [15-Apr-2011] $)


  $( The union of two transitive classes is transitive.  JFM CLASSES1. th. 55
     (Contributed by FL, 16-Apr-2011.) $)
  trunitr $p |- ( ( Tr A /\ Tr B ) -> Tr ( A u. B ) ) $=
    ( wtr wa cun cuni wss uniun df-tr unss12 syl2anb syl5eqss sylibr ) ACZBCZDZ
    ABEZFZQGQCPRAFZBFZEZQABHNSAGTBGUAQGOAIBISATBJKLQIM $.
    $( [16-Apr-2011] $)

  ${
    $d A x $.
    $( Union of a cumulative hierarchy of sets.  (Contributed by FL,
       23-Apr-2011.) $)
    uncum2 $p |- ( A e. On -> U_ x e. A ( R1 ` x ) C_ ( R1 ` A ) ) $=
      ( con0 wcel cv cr1 cfv wss wral ciun r1ord2 r19.21aiv iunss sylibr ) BCDZ
      AEZFGZBFGZHZABIABQJRHOSABPBKLABQRMN $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d A x y $.
    $( If all the elements of a set ` A ` are ordinal numbers and are parts of
       the set then ` A ` is an ordinal number.  (Contributed by FL,
       20-Apr-2011.) $)
    celsor $p |- ( ( A e. B /\ A. x e. A ( x e. On /\ x C_ A ) )
      -> A e. On ) $=
      ( vy wcel cv con0 wss wa wral wtr wceq w3o r19.26 dftr3 biimpri wi eleq1
      cbvralv raaanv word eloni ordtri3or syl2an ralimi sylbir expcom sylbi
      pm2.43i anim12i ancoms adantl wb elong adantr dford2 syl6bb mpbird ) BCEZ
      AFZGEZUTBHZIABJZIZBGEZBKZUTDFZEUTVGLVGUTEMZDBJZABJZIZVCVKUSVCVAABJZVBABJZ
      IVKVAVBABNVMVLVKVMVFVLVJVFVMABOPVLVJVLVGGEZDBJZVLVJQVAVNADBUTVGGRSVLVOVJV
      LVOIVAVNIZDBJZABJVJVAVNADBTVQVIABVPVHDBVAUTUAVGUAVHVNUTUBVGUBUTVGUCUDUEUE
      UFUGUHUIUJUKUHULVDVEBUAZVKUSVEVRUMVCBCUNUOADBUPUQUR $.
      $( [20-Apr-2011] $)
  $}


  ${
    $( If a relation ` R ` is reflexive, it is included in ` ( R o. R ) ` .
       (Contributed by FL, 8-May-2011.) $)
    reflincror $p |- ( ( Rel R /\ ( _I |` U. U. R ) C_ R )
       -> R C_ ( R o. R ) ) $=
      ( wrel cid cuni cres wss ccom wceq relcoi2 coss1 sseq1 biimpd syl2im imp
      ) ABZCADDEZAFZAAAGZFZOPAGZAHZQTRFZSAIPAAJUAUBSTARKLMN $.
      $( [8-May-2011] $)
  $}

  ${
    fldrels.1 $e |- X = U. U. R $.
    $( The field of a relation is a set.  (Contributed by FL, 23-May-2011.) $)
    fldrels $p |- ( R e. S -> X e. _V ) $=
      ( wcel cuni cvv uniexg eqcomi eleq1i biimpi 3syl ) ABEAFZGEMFZGEZCGEZABHM
      GHOPNCGCNDIJKL $.
      $( [23-May-2011] $)
  $}

  ${
    $( Value when ` C ` doesn't belong to the domain.  (Contributed by FL,
       26-Jun-2011.) $)
    fvsnn $p |- ( C =/= A -> ( { <. A , B >. } ` C ) = (/) ) $=
      ( wne cop csn cdm wcel wn cfv c0 wceq wi dmsnop elsni necon3ai eleq2
      notbid syl5ibr ax-mp ndmfv syl ) CADZCABEFZGZHZIZCUDJKLUEAFZLZUCUGMABNUCU
      GUICUHHZIUJCACAOPUIUFUJUEUHCQRSTCUDUAUB $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $d D x $.
    fvsn2.1 $e |- A e. E $.
    fvsn2.2 $e |- B e. F $.
    fvsn2.3 $e |- C e. G $.
    fvsn2.4 $e |- D e. H $.
    $( Value of a function with a domain of two different values.  (Contributed
       by FL, 26-Jun-2011.) $)
    fvsn2a $p |- ( A =/= B ->
       ( { <. A , C >. , <. B , D >. } ` A ) = C ) $=
      ( elisseti fvpr1 ) ABCDAEIMBFJMCGKMDHLMN $.
      $( [26-Jun-2011] $)

    $( Value of a function with a domain of two different values.  (Contributed
       by FL, 26-Jun-2011.) $)
    fvsn2b $p |- ( A =/= B ->
       ( { <. A , C >. , <. B , D >. } ` B ) = D ) $=
      ( elisseti fvpr2 ) ABCDAEIMBFJMCGKMDHLMN $.
      $( [26-Jun-2011] $)
  $}

  $( A relation empty iff its converse is empty.  (Contributed by FL,
     19-Sep-2011.) $)
  cnveq3 $p |- ( Rel A -> ( A = (/) <-> `' A = (/) ) ) $=
    ( c0 ccnv wceq wrel wb wi cnv0 rel0 cnveqb mpan2 eqeq2 bibi2d syl5ibr
    eqcoms ax-mp ) BCZBDAEZABDZACZBDZFZGZHUCBQRUBBQDZSTQDZFZRBEUFIABJKUDUAUESBQ
    TLMNOP $.
    $( [19-Sep-2011] $)

  ${
    $d R x $.
    $( A relation is reflexive iff its converse is reflexive.  (Contributed by
       FL, 19-Sep-2011.) $)
    relrefcnv $p |- ( Rel R -> ( ( _I |` U. U. R ) C_ R <->
      ( _I |` U. U. `' R ) C_ `' R ) ) $=
      ( vx wrel cv wbr cuni wral ccnv cid cres wss cnvref2 ref4w 3bitr3g ) ACBD
      ZOAEBAFFZGOOAHZEBQFFZGIPJAKIRJQKBALBPAMBRQMN $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F i x $.  $d G i x $.  $d I x $.
    $( If a family of sets ` A ` indexed by ` I ` covers the common domain
       ` B ` of two functions ` F ` and ` G ` , the restrictions of ` F ` and
       ` G ` to ` ( A i^i B ) ` are equal iff ` F = G ` .  Compare ~ eqfnun .
       (Contributed by FL, 5-Nov-2011.) $)
    eqfnung2 $p |- ( ( B C_ U_ i e. I A /\ F Fn B /\ G Fn B ) ->
    ( A. i e. I ( F |` A ) = ( G |` A ) <-> F = G ) ) $=
      ( vx ciun wss wfn w3a cres wceq wral wa cv cfv eqidd wcel wi ssel wrex
      eliun r19.29 fveq1 fvres eqeq12 biimpd syl11anc mpan9 r19.23aivw a1d a1i
      a1i12 com4r syl ex com4t 3imp com3l sylbi syl6com pm2.43a com14 pm2.43i
      3expd 3imp1 r19.21aiv jca wb 3simpc adantr eqfnfv mpbird reseq1
      r19.21aiva impbid1 ) BCFAHZIZDBJZEBJZKZDALZEALZMZCFNZDEMZWBWFWGWBWFOZWGBB
      MZGPZDQZWJEQZMZGBNZOZWHWIWNWHBRWHWMGBVSVTWAWFWJBSZWMTZVSVTWAWFWQVSVTWAWFK
      ZWQTWPVSWRVSWMVSWPWRVSWMTZTZVSWPWJVRSZWPWTTZBVRWJUAXAWJASZCFUBZXBCWJFAUCW
      RXDWPWSVTWAWFXDWPWSTZTWFXDVTWAXEWFXDVTWAXETTZWFXDOWEXCOZCFUBZXFWEXCCFUDVT
      WAWPXHWSVTWAWPXHWSTZTXIWPXHWMVSXGWMCFWEWJWCQZWJWDQZMZXCWMWJWCWDUEXCXJWKMZ
      XKWLMZXLWMTWJADUFWJAEUFXMXNOXLWMXJWKXKWLUGUHUIUJUKULUMUNUOUPUQURUSUTVAVBV
      CVDVEVFVGVHVIWHVTWAOZWGWOVJWBXOWFVSVTWAVKVLGBBDEVMUPVNUQWGWECFWGWECPFSDEA
      VOVLVPVQ $.
      $( [5-Nov-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d R x y $.
    $( An application is injective if a retraction exists.  Bourbaki E.II.18
       prop. 8.  (Contributed by FL, 11-Nov-2011.) $)
    injrec $p |- ( ( F : A --> B /\ Fun R /\ ( R o. F ) = ( _I |` A ) )
         -> F : A -1-1-> B ) $=
      ( vx vy wf wfun ccom cid cres wceq w3a cv cfv weq wi wral wf1 simp1 wcel
      wa cdm fdm wb eleq2 anbi12d eqcoms biimpd ffun fvco 3expia anim12d fveq2
      eqtr fveq1 fvresi ex syl com3l imp syl2an syl5com syl2im com23 mpcom
      expcom com4l com14 syl6 com24 com15 syli com25 3imp r19.21aivv dff13
      sylanbrc ) ABDGZCHZCDIZJAKZLZMZVSENZDOZFNZDOZLZEFPZQZFAREARABDSVSVTWCTWDW
      KEFAAVSVTWCWEAUAZWGAUAZUBZWKQZDUCZALZVSVTWCWOQZQABDUDWQWNVTWCVSWKWNWQWEWP
      UAZWGWPUAZUBZVTWCVSWKQQQWQWNXAWNXAUEAWPAWPLWLWSWMWTAWPWEUFAWPWGUFUGUHUIVS
      WNVTWCXAWKVSDHZWNVTWCXAWKQQZQQABDUJVTXBWNXCVTXBWNXCQVTXBUBZXAWCWNWKXDXAWE
      WAOZWFCOZLZWGWAOZWHCOZLZUBZWRXDWSXGWTXJVTXBWSXGWECDUKULVTXBWTXJWGCDUKULUM
      WIWCWNXKWJWIXFXILZWCWNXKWJQQQWFWHCUNXKXLWCWNWJXGXJXLWCWNWJQZQZQXGXLXJXNXG
      XLXJXNQZXGXLUBXEXILXOXEXFXIUOXOXIXEXJXIXELZXNXJXPUBXHXELZWCXMXHXIXEUOXHWG
      WBOZLWCXQXMQZWGWAWBUPWCXSQXRXHXRXHLZXQWCXMXTXQXNXTXQUBXRXELZWCXEWEWBOZLZX
      MXRXHXEUOWEWAWBUPYAYCXMYAYCUBXRYBLZWNWJXRXEYBUOWLYBWELZXRWGLZYDWJQZWMAWEU
      QAWGUQYEYFYGYDYEYFWJYDYEYFWJQZYDYEUBXRWELYHXRYBWEUOYHWEXRWEXRLYFWJWEXRWGU
      OURUHUSURUTVAVBVCURVDURVEUHVFVCVGUHUSURVEVAVHUSVIVJVKURUTUSVLVMVNVFVOVPEF
      ABDVQVR $.
      $( [11-Nov-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d S x y $.
    $( An application is surjective if a section exists.  Bourbaki E.II.18
       prop. 8.  (Contributed by FL, 17-Nov-2011.) $)
    surjsec $p |- ( ( F : A --> B /\ S : B --> A /\ ( F o. S ) = ( _I |` B ) )
         -> F : A -onto-> B ) $=
      ( vy vx wf ccom cid cres wceq w3a cv cfv wrex wral wfo simp1 wcel wi
      fveq1 wa fvres fvi jca eqtr wfun cdm ffun adantl 3ad2ant1 adantr fdm
      eleq2 biimpd eqcoms syl imp 3adant2 3jca fvco ffvelrn fveq2 eqeq2d
      rcla4ev ex com13 com23 a1i com4l com12 3imp 3syl com14 pm2.43i com3l
      3exp mpcom r19.21aiv dffo3 sylanbrc ) ABDGZBACGZDCHZIBJZKZLZWBEMZFMZDNZKZ
      FAOZEBPABDQWBWCWFRWGWLEBWBWCWFWHBSZWLTZWFWBWCWNWFWHWDNZWHWENZKZWBWCWNTZTW
      HWDWEUAWMWQWBWCWLWPWHINZKZWSWHKZUBZWMWQWBWCWLTZTZTZWMWTXAWHBIUCWHBUDUEXBW
      PWHKZWMXETWPWSWHUFWQXFWMXDWQXFWMXDTZWQXFUBWOWHKZXGWOWPWHUFWCXHWMWBWLWCXHW
      MWBWLTZWCXHWMLZXIWBXJXJWLWBXJXJWLTTXJWBXJWBWLXJWBXJXITZXJWBUBZDUGZCUGZWHC
      UHZSZLWOWHCNZDNZKZXKXLXMXNXPWBXMXJABDUIUJXJXNWBWCXHXNWMBACUIUKULXJXPWBWCW
      MXPXHWCWMXPWCXOBKWMXPTZBACUMXTBXOBXOKWMXPBXOWHUNUOUPUQURUSULUTWHDCVAXJXSX
      IWCXHWMXSXITZXHWCWMYATXSXHWCWMXIXHWCWMXITTZTXRWOXRWOKZXHYBYCXHUBXRWHKZYBX
      RWOWHUFWBYDWCWMWLYDWRTWBYDWMWCWLWMXCTWHXRWCWMWHXRKZWLWCWMYEWLTZWCWMUBXQAS
      ZYFBAWHCVBYGYEWLWKYEFXQAWIXQKWJXRWHWIXQDVCVDVEVFUQVFVGUPVHVIVJUQVFUPVJVKV
      LVKVMVFVNVOVPVOVQVJUQVFVPUQVRVJUQVPVLVSFEABDVTWA $.
      $( [17-Nov-2011] $)
  $}

  ${
    $d A r $.  $d B r $.  $d F r $.
    $( A function is an injection iff a retraction exists.  Bourbaki E.II.18
       prop. 8.  (Contributed by FL, 11-Nov-2011.) $)
    injrec2 $p |- ( ( F : A --> B /\ A e. C )
      -> ( F : A -1-1-> B <-> E. r ( Fun r /\
        ( r o. F ) = ( _I |` A ) ) ) ) $=
      ( wf wcel wa wf1 cv wfun ccom cid cres wceq wex ccnv cvv fex cnvexg syl
      adantr df-f1 simprbi adantl f1cocnv1 jca32 ex funeq coeq1 eqeq1d anbi12d
      cla4egv imp syl6 wi injrec 3exp com3l 19.23aiv com12 impbid ) ABDFZACGZHZ
      ABDIZEJZKZVGDLZMANZOZHZEPZVEVFDQZRGZVNKZVNDLZVJOZHZHZVMVEVFVTVEVFHVOVPVRV
      EVOVFVEDRGVOABCDSDRTUAUBVFVPVEVFVCVPABDUCUDUEVFVRVEABDUFUEUGUHVOVSVMVLVSE
      VNRVGVNOZVHVPVKVRVGVNUIWAVIVQVJVGVNDUJUKULUMUNUOVCVMVFUPVDVMVCVFVLVCVFUPZ
      EVHVKWBVCVHVKVFVCVHVKVFABVGDUQURUSUNUTVAUBVB $.
      $( [11-Nov-2011] $)
  $}

  ${
    $d A a b s $.  $d B b s $.  $d F a b s x y $.
    $( A function is an surjection iff a section exists.  Bourbaki E.II.18
       prop. 8.  (Contributed by FL, 18-Nov-2011.) $)
    surjsec2 $p |- ( ( F : A --> B /\ A e. C /\ B e. D )
      -> ( F : A -onto-> B <-> E. s ( s : B --> A /\
        ( F o. s ) = ( _I |` B ) ) ) ) $=
      ( va vb vx vy wf wcel wfo cv ccom cid cres wceq wa wex wb wi feq2 foeq2
      feq3 anbi1d exbidv bibi12d imbi12d foeq3 reseq2 eqeq2d anbi12d cfv wral
      wrex dffo3 eqcom biimpi reximi ralimi adantl sylbi visset fveq2 eqeq1d
      ac6 syl wfn fofn fnfco sylan fnresi eqfnfv2 sylancl wel w3a wfun cdm
      fofun 3ad2ant1 ffun 3ad2ant2 fdm eqcomd eleq2d biimpa 3adant1 fvco
      syl111anc fvresi 3ad2ant3 eqeq12d 3expa ralbidva bitrd pm5.32da mpbird
      surjsec 3exp com3l imp 19.23aiv com12 impbid2 vtocl2g ex com3r 3imp ) ABE
      KZACLZBDLZABEMZBAFNZKZEXNOZPBQZRZSZFTZUAZXKXLXJYAXKXLXJYAUBZGNZHNZEKZYCYD
      EMZYDYCXNKZXPPYDQZRZSZFTZUAZUBAYDEKZAYDEMZYDAXNKZYISZFTZUAZUBYBGHABCDYCAR
      ZYEYMYLYRYCAYDEUCYSYFYNYKYQYCAYDEUDYSYJYPFYSYGYOYIYCAYDXNUEUFUGUHUIYDBRZY
      MXJYRYAYDBAEUEYTYNXMYQXTYDBAEUJYTYPXSFYTYOXOYIXRYDBAXNUCYTYHXQXPYDBPUKULU
      MUGUHUIYEYFYKYFYKYGINZXNUNZEUNZUUARZIYDUOZSZFTZYFJNZEUNZUUARZJYCUPZIYDUOZ
      UUGYFYEUUAUUIRZJYCUPZIYDUOZSUULJIYCYDEUQUUOUULYEUUNUUKIYDUUMUUJJYCUUMUUJU
      UAUUIURUSUTVAVBVCUUJUUDIJYDYCFHVDGVDUUHUUBRUUIUUCUUAUUHUUBEVEVFVGVHYFYJUU
      FFYFYGYIUUEYFYGSZYIUUAXPUNZUUAYHUNZRZIYDUOZUUEUUPXPYDVIZYHYDVIYIUUTUAYFEY
      CVIYGUVAYCYDEVJYCYDEXNVKVLYDVMIYDXPYHVNVOUUPUUSUUDIYDYFYGIHVPZUUSUUDUAYFY
      GUVBVQZUUQUUCUURUUAUVCEVRZXNVRZUUAXNVSZLZUUQUUCRYFYGUVDUVBYCYDEVTWAYGYFUV
      EUVBYDYCXNWBWCYGUVBUVGYFYGUVBUVGYGYDUVFUUAYGUVFYDYDYCXNWDWEWFWGWHUUAEXNWI
      WJUVBYFUURUUARYGYDUUAWKWLWMWNWOWPWQUGWRYKYEYFYJYEYFUBZFYGYIUVHYEYGYIYFYEY
      GYIYFYCYDXNEWSWTXAXBXCXDXEXFXGXHXI $.
      $( [18-Nov-2011] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d F x y $.
    $( Preimage of an intersection.  (Contributed by FL, 16-Apr-2012.) $)
    inpreima5 $p |- ( ( Fun F /\ A =/= (/) )
        -> ( `' F " |^|_ x e. A B ) = |^|_ x e. A ( `' F " B ) ) $=
      ( vy wfun c0 wne wa ccnv ciin cima cv wcel wral cdm cfv simpll cnvimass
      sseli adantl cvv fvex fvimacnvi adantlr eliin biimpa sylancr fvimacnv
      ralbidv syl21anc wb visset ax-mp sylibr wi biimpd ex ralimdv sylc wrex
      r19.2zb biimpi r19.23aivw syl6 syl5bi imp syl11anc mpbid impbida eqrdv )
      DFZBGHZIZEDJZABCKZLZABVOCLZKZVNEMZVQNZVTVSNZVNWAIZVTVRNZABOZWBWCVLVTDPZNZ
      VTDQZCNZABOZWEVLVMWARWAWGVNVQWFVTDVPSTUAWCWHUBNZWHVPNZWJVTDUCZVLWAWLVMVTV
      PDUDUEWKWLWJAWHBCUBUFZUGUHVLWGIZWJWEWOWIWDABVTCDUIUJUGUKVTUBNZWBWEULEUMZA
      VTBVRUBUFZUNZUOVNWBIZWLWAWTWJWLWTVLWEWJVLVMWBRZWBWEVNWPWBWEUPWQWPWBWEWRUQ
      UNUAVLWDWIABVLWDWIVTCDUDURUSUTWKWLWJULWMWNUNUOWTVLWGWLWAULXAVNWBWGVMWBWGU
      PVLWBWEVMWGWSVMWEWDABVAZWGVMWEXBUPWDABVBVCWDWGABVRWFVTDCSTVDVEVFUAVGVTVPD
      UIVHVIVJVK $.
      $( [16-Apr-2012] $)
  $}

  ${
    $d A x $.  $d F x $.
    $( Preimage of an intersection.  (Contributed by FL, 28-Apr-2012.) $)
    inpreima2 $p |- ( ( Fun F /\ A =/= (/) )
        -> ( `' F " |^| A ) = |^|_ x e. A ( `' F " x ) ) $=
      ( wfun c0 wne wa ccnv cint cima cv ciin intiin imaeq2i inpreima5 syl5eq
      ) CDBEFGCHZBIZJQABAKZLZJABQSJLRTQABMNABSCOP $.
      $( [28-Apr-2012] $)
  $}

  ${
    $d A x z $.  $d B y z $.  $d C z $.  $d D x z $.
    $( Existence of a class abstraction of existentially restricted sets.
       (Contributed by FL, 15-Oct-2012.) $)
    ab2rexexg2 $p |- ( ( A e. D /\ A. x e. A B e. E )
       -> { z | E. x e. A E. y e. B z = C } e. _V ) $=
      ( wcel wral cv wceq wrex cab cvv abrexexg ralimi abrexex2g sylan2 ) EHIZA
      DJDGICKFLBEMZCNOIZADJUAADMCNOITUBADBCEFHPQUAACDGORS $.
      $( [15-Oct-2012] $)
  $}


  ${
    $d A x z $.  $d B x z $.  $d B y z $.  $d C z $.
    $( Can I remove the distinct variables statement @d B x @. $)
    $( Existence of a class abstraction of existentially restricted sets.
       (Contributed by FL, 19-Apr-2012.) $)
    ab2rexexg $p |- ( ( A e. D /\ B e. E )
       -> { z | E. x e. A E. y e. B z = C } e. _V ) $=
      ( wcel cvv cv wceq wrex cab elisset c0 cif ax-17 rexeq abbid eleq1d wb
      adantr rexbidva 0ex elimel ab2rexex dedth2h syl2an ) DGIDJIZEJIZCKFLZBEMZ
      ADMZCNZJIZEHIDGOEHOUJUKUPUMAUJDPQZMZCNZJIULBUKEPQZMZAUQMZCNZJIDEPPDUQLZUO
      USJVDUNURCVDCRUMADUQSTUAEUTLZUSVCJVEURVBCVECRVEUMVAAUQVEUMVAUBAKUQIULBEUT
      SUCUDTUAABCUQUTFDPJUEUFEPJUEUFUGUHUI $.
      $( [19-Apr-2012] $)
  $}

  $( Equality theorem for one-to-one onto functions.  (Contributed by FL,
     14-Jul-2012.) $)
  f1oeq23 $p |- ( ( A = B /\ C = D ) ->
   ( F : A -1-1-onto-> C <-> F : B -1-1-onto-> D ) ) $=
    ( wceq wf1o f1oeq2 f1oeq3 sylan9bb ) ABFACEGBCEGCDFBDEGABCEHCDBEIJ $.
    $( [14-Jul-2012] $)


  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d D x y z $.
    oprabval2gc.1 $e |- O = { <. <. x , y >. , z >. |
      ( x e. C /\ y e. D /\ z = ( x o. y ) ) } $.
    $( Value of a composition. ~ oprabval2g adapted to this special case of a
       composite.  (Contributed by FL, 14-Jul-2012.) $)
    oprabval2gc $p |- ( ( A e. C /\ B e. D ) -> ( A O B ) = ( A o. B ) ) $=
      ( wcel ccom cvv co wceq coexg cv coeq1 coeq2 w3a copab2 wa df-3an
      oprabbii eqtri oprabval2g mpd3an3 ) DFJEGJDEKZLJDEHMUGNDEFGOABCDEFGAPZBPZ
      KZUGHDUIKLUHDUIQUIEDRHUHFJZUIGJZCPUJNZSZABCTUKULUAUMUAZABCTIUNUOABCUKULUM
      UBUCUDUEUF $.
      $( [14-Jul-2012] $)
  $}

  ${
    $d C x y z $.  $d D x y z $.
    oprabval4gc.1 $e |- O = { <. <. x , y >. , z >. |
      ( x e. C /\ y e. D /\ z = ( x o. y ) ) } $.
    $( Value of a composition. ~ oprabval4g adapted to the special case of a
       composite.  (Contributed by FL, 14-Jul-2012.) $)
    oprabval4gc $p |- ( ( x e. C /\ y e. D ) -> ( x O y ) = ( x o. y ) ) $=
      ( cv wcel ccom cvv co wceq coexg w3a copab2 wa df-3an oprabbii eqtri
      oprabval4g mpd3an3 ) AHZDIZBHZEIZUCUEJZKIUCUEFLUGMUCUEDENABCDEUGKFFUDUFCH
      UGMZOZABCPUDUFQUHQZABCPGUIUJABCUDUFUHRSTUAUB $.
      $( [14-Jul-2012] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C z $.
    eloprvdm.1 $e |-
      F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ z = C ) } $.
    eloprvdm.2 $e |- C e. H $.
    $( If an operation value has a member, the first argument belongs to the
       first projection of the domain.  (Contributed by FL, 15-Oct-2012.) $)
    eloprvdm $p |- ( D e. ( E F G ) -> E e. A ) $=
      ( wcel cop cfv co cdm elfvdm cxp opelxp1 cv wa wceq copab2 wrel relxp
      dmoprabss6 ax-mp wb wi twsvbdop eqtr dmeq eqeq1d syl expcom eqcoms
      mpbiri eleq2s df-opr ) HDNZGHJOZIPZHJIQGVDNVCIRZNVBGVCISVBVCDETZVEHJDEUAI
      AUBZDNBUBZENUCCUBFUDZUCABCUEZUDZVEVFUDZLVKVLVGVHOVFNVIUCABCUEZRZVFUDZVFUF
      VODEUGABCVFFKMUHUIVMVJUDVKVLVOUJZUKZVIABCDEULVQVJVMVKVJVMUDZVPVKVRUCIVMUD
      ZVPIVJVMUMVSVEVNVFIVMUNUOUPUQURUIUSUIUTUPHJIVAUT $.
      $( [15-Oct-2012] $)

    $( If an operation value has a member, the second argument belongs to the
       second projection of the domain.  (Contributed by FL, 15-Oct-2012.) $)
    eloprvdm2 $p |- ( G e. I ->
       ( D e. ( E F G ) -> ( E e. A /\ G e. B ) ) ) $=
      ( wcel co cop cxp wa wi cfv cdm elfvdm cv wceq copab2 dmoprabss sseli
      dmeqi eleq2s a1i syl5com df-opr com12 opelxpg sylibd ) JLOZGHJIPZOZHJQZDE
      RZOZHDOJEOSUSUQVBUQVBTGUTIUAZURGVCOUTIUBZOZUQVBGUTIUCVEVBTUQVBUTAUDDOBUDE
      OSCUDFUEZSABCUFZUBZVDVHVAUTVFABCDEUGUHIVGMUIUJUKULHJIUMUJUNHJDELUOUP $.
      $( [15-Oct-2012] $)

  $}

  ${
    $d A y $.  $d R x y $.  $d i y $.
    $( The domain of the intersection of a family of reflexive classes is the
       intersection of the domains.  (Contributed by FL, 15-Oct-2012.) $)
    domintrefc $p |- ( A. i e. A A. x e. dom R x R x ->
      dom |^|_ i e. A R = |^|_ i e. A dom R ) $=
      ( vy cv wbr cdm wral ciin wss dmiin a1i wcel wi cvv wb visset eliin
      ax-mp wa r19.26 weq id breq12d rcla4va ralimi cop df-br ralbii opex
      bicomd opeldm sylbi syl sylbir ex com12 r19.21aiv dfss3 sylibr eqssd ) AF
      ZVCCGZACHZIZDBIZDBCJZHZDBVEJZVIVJKVGDBCLMVGEFZVINZEVJIVJVIKVGVLEVJVKVJNZV
      GVLVMVKVENZDBIZVGVLOVKPNVMVOQERZDVKBVEPSTVOVGVLVOVGUAVNVFUAZDBIZVLVNVFDBU
      BVRVKVKCGZDBIZVLVQVSDBVDVSAVKVEAEUCZVCVKVCVKCWAUDZWBUEUFUGVTVKVKUHZCNZDBI
      ZVLVSWDDBVKVKCUIUJWEWCVHNZVLWCPNZWEWFQVKVKUKWGWFWEDWCBCPSULTVKVKVHVPUMUNU
      NUOUPUQUNURUSEVJVIUTVAVB $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d A x $.
    $( The range of an intersection is a part of the intersection of the
       ranges.  (The case ` A = (/) ` works as well, the intersection gives
       ` _V ` ).  (Contributed by FL, 15-Oct-2012.) $)
    rnintintrn $p |- ran |^|_ x e. A B C_ |^|_ x e. A ran B $=
      ( ciin crn wss c0 wceq cvv iineq1 0iin wa eqtr ssv sseq2 mpbiri syl
      sylancl wne ccnv cdm cnviin dmeqd dmiin a1i eqsstrd df-rn cv wcel
      iineq2i 3sstr4g pm2.61ine ) ABCDZEZABCEZDZFZBGBGHUPAGUODZHZURIHZUQABGUOJA
      UOKUSUTLUPIHZUQUPURIMVAUQUNIFUNNUPIUNOPQRBGSZUMTZUAZABCTZUAZDZUNUPVBVDABV
      EDZUAZVGVBVCVHABCUBUCVIVGFVBABVEUDUEUFUMUGABUOVFUOVFHAUHBUICUGUEUJUKUL $.
      $( [15-Oct-2012] $)
  $}

  $( Projection of a part of a cross product.  (Contributed by FL,
     15-Oct-2012.) $)
  prjpacp1 $p |- ( ( B =/= (/) /\ C C_ ( A X. B ) ) -> ( 1st " C ) C_ A ) $=
    ( cxp wss c0 wne c1st cima imass2 wceq wi prjcp1 sseq2 biimpd syl syl5com
    impcom ) CABDZEZBFGZHCIZAEZTUBHSIZEZUAUCCSHJUAUDAKZUEUCLABMUFUEUCUDAUBNOPQR
    $.
    $( [15-Oct-2012] $)

  $( Projection of a part of a cross product.  (Contributed by FL,
     15-Oct-2012.) $)
  prjpacp2 $p |- ( ( A =/= (/) /\ C C_ ( A X. B ) ) -> ( 2nd " C ) C_ B ) $=
    ( cxp wss c0 wne c2nd cima imass2 wceq wi prjcp2 sseq2 biimpd syl syl5com
    impcom ) CABDZEZAFGZHCIZBEZTUBHSIZEZUAUCCSHJUAUDBKZUEUCLABMUFUEUCUDBUBNOPQR
    $.
    $( [15-Oct-2012] $)

  $( Composition of the first member function with another function.
     (Contributed by FL, 15-Oct-2012.) $)
  2ndcof $p |- ( F : A --> ( B X. C ) -> ( 2nd o. F ) : A --> C ) $=
    ( cxp wf c2nd ccom wfn crn wss cvv wfo fo2nd fofn ax-mp ffn dffn2 sylib
    fnfco sylancr cres rnco frn ssres2 rnss 3syl f2ndres a1i sstrd syl5eqss
    df-f sylanbrc ) ABCEZDFZGDHZAIZUPJZCKACUPFUOGLIZALDFZUQLLGMUSNLLGOPUODAIUTA
    UNDQADRSLAGDTUAUOURGDJZUBZJZCGDUCUOVCGUNUBZJZCUOVAUNKVBVDKVCVEKAUNDUDVAUNGU
    EVBVDUFUGVECKZUOUNCVDFVFBCUHUNCVDUDPUIUJUKACUPULUM $.
    $( [15-Oct-2012] $)

  $( A relation is included in the cross product of its projections.
     (Contributed by FL, 15-Oct-2012.) $)
  relinccppr $p |- ( Rel A -> A C_ ( ( 1st " A ) X. ( 2nd " A ) ) ) $=
    ( wrel c1st cima c2nd cxp wss cdm wceq crn wi prjdmn prjrn wa xpeq12
    relssdmrn sseq2 syl5ibr syl syl11anc pm2.43i ) ABZACADZEADZFZGZUBUCAHZIZUDA
    JZIZUBUFKZALAMUHUJNUEUGUIFZIZUKUCUGUDUIOUBUFUMAULGAPUEULAQRSTUA $.
    $( [15-Oct-2012] $)

  ${
    $d x y z w A $.  $d x y z a $.  $d y z w F $.  $d F a $.
    dffn5a.1 $e |- ( a e. F -> A. x a e. F ) $.
    $( Representation of a function in terms of its values.  (Contributed by
       FL, 14-Sep-2013.) $)
    dffn5a $p |- ( F Fn A <->
                   F = { <. x , y >. | ( x e. A /\ y = ( F ` x ) ) } ) $=
      ( vz vw wfn cv wcel cfv wceq wa copab cop wb wal fnop ex pm4.71rd eqcom
      visset fnopfvb syl5bb pm5.32da bitr4d ax-17 hbfv hbeq hban eleq1 fveq2
      eqeq2d anbi12d biidd eqeq1 opelopabf syl6bbr 19.21aivv wrel fnrel
      relopab eqrel sylancl mpbird fvex eqid fnopab2 fneq1 mpbiri impbii ) DCIZ
      DAJZCKZBJZVNDLZMZNZABOZMZVMWAGJZHJZPZDKZWDVTKZQZHRGRZVMWGGHVMWEWBCKZWCWBD
      LZMZNZWFVMWEWIWENWLVMWEWIVMWEWICWBWCDSTUAVMWIWKWEWKWJWCMVMWINWEWCWJUBCWBW
      CDHUCZUDUEUFUGVSWIVPWJMZNWLABWBWCWIWNAWIAUHAEEVPWJEJZVPKAUHAEWBDFWOWBKAUH
      UIUJUKWLBUHGUCWMVNWBMZVOWIVRWNVNWBCULWPVQWJVPVNWBDUMUNUOVPWCMZWIWIWNWKWQW
      IUPVPWCWJUQUOURUSUTVMDVAVTVAWAWHQCDVBVSABVCGHDVTVDVEVFWAVMVTCIABCVQVTVNDV
      GVTVHVICDVTVJVKVL $.
      $( [14-Sep-2013] $)
  $}

  $( A function's value belongs to its codomain.  (Contributed by FL,
     14-Sep-2013.) $)
  ffvelrnb $p |- ( ( A e. D /\ B e. E ) -> ( ( F e. ( B ^m A ) /\ C e. A )
    -> ( F ` C ) e. B ) ) $=
    ( wcel wa cmap co cfv wf wi wb elmapg ancoms ffvelrn ex syl6bi imp3a ) ADGZ
    BEGZHZFBAIJGZCAGZCFKBGZUCUDABFLZUEUFMUBUAUDUGNBAFEDOPUGUEUFABCFQRST $.
    $( [14-Sep-2013] $)

  $( End of General Set Theory. $)

