$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Homsets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c hom $.

  $( Extend class notation with the function returning all the morphisms
     between two objects. $)
  chom $a class hom $.

  ${
    $d x y a b c f $.
    $( ` ( hom `` x ) ` is a function which returns for each pair of objects
       ` <. a , b >. ` the morphisms whose domain is ` a ` and codomain
       ` b ` .  JFM CAT_1 def. 6 $)
    df-hom $a |- hom = { <. x , y >. | ( x e. Cat /\
     y = { <. <. a , b >. , c >. | ( a e. dom ( id_ ` x ) /\
       b e. dom ( id_ ` x ) /\
       c = { f | ( f e. dom ( dom_ ` x ) /\ ( ( dom_ ` x ) ` f ) = a /\
                   ( ( cod_ ` x ) ` f ) = b ) } ) } ) } $.
  $}

  ${
    $d C c x y $.  $d D c x y $.  $d M c x y $.  $d O x y $.
    $d T a b c f x y $.
    ishoma.1 $e |- O = dom ( id_ ` T ) $.
    ishoma.2 $e |- M = dom ( dom_ ` T ) $.
    ishoma.3 $e |- D = ( dom_ ` T ) $.
    ishoma.4 $e |- C = ( cod_ ` T ) $.
    $( Definition of ` ( hom `` T ) ` . $)
    ishoma $p |- ( T e. Cat -> ( hom ` T ) = { <. <. a , b >. , c >. |
      ( a e. O /\ b e. O /\ c = { f | ( f e. M /\ ( D ` f ) = a /\
      ( C ` f ) = b ) } ) } ) $=
      ( vx vy cv cid_ cfv cdm wcel cdom_ wceq ccod_ w3a cab copab2 ccat chom
      fveq2 dmeqd syl6eqr eleq2d fveq1d eqeq1d 3anbi123d abbidv eqeq2d
      oprabbidv df-hom fvex dmex wa eleq2i biid 3anbi123i df-3an bitri
      oprabbii oprabex2 fvopab4 ) NOCGPZNPZQRZSZTZHPZVNTZIPZDPZVLUARZSZTZVSVTRZ
      VKUBZVSVLUCRZRZVPUBZUDZDUEZUBZUDZGHIUFVKFTZVPFTZVRVSETZVSBRZVKUBZVSARZVPU
      BZUDZDUEZUBZUDZGHIUFZUGUHVLCUBZWKXBGHIXDVOWLVQWMWJXAXDVNFVKXDVNCQRZSZFXDV
      MXEVLCQUIUJJUKZULXDVNFVPXGULXDWIWTVRXDWHWSDXDWBWNWDWPWGWRXDWAEVSXDWACUARZ
      SEXDVTXHVLCUAUIZUJKUKULXDWCWOVKXDVSVTBXDVTXHBXILUKUMUNXDWFWQVPXDVSWEAXDWE
      CUCRAVLCUCUIMUKUMUNUOUPUQUOURNODGHIUSGHIXFXFWTXCXECQUTVAZXJXBVKXFTZVPXFTZ
      VBXAVBZGHIXBXKXLXAUDXMWLXKWMXLXAXAFXFVKJVCFXFVPJVCXAVDVEXKXLXAVFVGVHVIVJ
      $.
      $( [6-May-2007] $)
  $}

  ${
    $d A f x y z $.  $d B f x y z $.  $d C x y z $.  $d D x y z $.
    $d M f x y z $.  $d O x y z $.  $d T f x y z $.
    ishomb.1 $e |- O = dom ( id_ ` T ) $.
    ishomb.2 $e |- M = dom ( dom_ ` T ) $.
    ishomb.3 $e |- D = ( dom_ ` T ) $.
    ishomb.4 $e |- C = ( cod_ ` T ) $.
    ishomb.5 $e |- H = ( hom ` T ) $.
    ishomb.6 $e |- T e. Cat $.
    $( The homset ` ( ( hom `` T ) `` <. A , B >. ) ` . $)
    ishomb $p |- ( ( A e. O /\ B e. O ) ->
      ( H ` <. A , B >. ) = { f | ( f e. M /\ ( D ` f ) = A /\
      ( C ` f ) = B ) } ) $=
      ( vx vy vz wcel wa co cv cfv wceq w3a cab cop cvv 3anass abbii cdom_ cdm
      fvex dmex eqeltri zfausab eqeq2 3anbi2d abbidv 3anbi3d chom copab2 ccat
      ishoma wb df-3an a1i oprabbidv eqtrd ax-mp eqtri oprabval2 df-opr
      syl5eqr ) AISBISTABGUAFUBZHSZVODUCZAUDZVOCUCZBUDZUEZFUFZABUGGUCPQRABIIVPV
      QPUBZUDZVSQUBZUDZUEZFUFZWBGVPVRWFUEZFUFWBVPVRVTTZTZFUFUHWAWKFVPVRVTUIUJWJ
      FHHEUKUCZULUHKWLEUKUMUNUOUPUOWCAUDZWGWIFWMWDVRVPWFWCAVQUQURUSWEBUDZWIWAFW
      NWFVTVPVRWEBVSUQUTUSGEVAUCZWCISZWEISZTRUBWHUDZTZPQRVBZNEVCSZWOWTUDOXAWOWP
      WQWRUEZPQRVBWTCDEFHIPQRJKLMVDXAXBWSPQRXBWSVEXAWPWQWRVFVGVHVIVJVKVLABGVMVN
      $.
      $( [18-May-2007] $)
  $}

  ${
    $d A x $.  $d B x $.  $d M x $.  $d D x $.  $d C x $.  $d T x $.  $d F x $.
    ishomc.1 $e |- O = dom ( id_ ` T ) $.
    ishomc.2 $e |- M = dom ( dom_ ` T ) $.
    ishomc.3 $e |- D = ( dom_ ` T ) $.
    ishomc.4 $e |- C = ( cod_ ` T ) $.
    ishomc.5 $e |- H = ( hom ` T ) $.
    ishomc.6 $e |- T e. Cat $.
    $( The predicate ` F e. ( ( hom `` T ) `` <. A , B >. ) ` JFM vol. 1.2
       p. 411 th. 18. $)
    ishomc $p |- ( ( A e. O /\ B e. O ) ->
      ( F e. ( H ` <. A , B >. ) <->
        ( F e. M /\ ( D ` F ) = A /\ ( C ` F ) = B ) ) ) $=
      ( vx wcel wa cop cfv cv wceq w3a cab ishomb eleq2d wi wb 3simp1 eleq1
      fveq2 eqeq1d 3anbi123d elab3g ax-mp syl6bb ) AIQBIQRZFABSGTZQFPUAZHQZUSDT
      ZAUBZUSCTZBUBZUCZPUDZQZFHQZFDTZAUBZFCTZBUBZUCZUQURVFFABCDEPGHIJKLMNOUEUFV
      MVHUGVGVMUHVHVJVLUIVEVMPFHUSFUBZUTVHVBVJVDVLUSFHUJVNVAVIAUSFDUKULVNVCVKBU
      SFCUKULUMUNUOUP $.
      $( [30-Nov-2007] $)
  $}

  ${
    ishomd.1 $e |- O = dom ( id_ ` T ) $.
    ishomd.2 $e |- M = dom ( dom_ ` T ) $.
    ishomd.3 $e |- D = ( dom_ ` T ) $.
    ishomd.4 $e |- C = ( cod_ ` T ) $.
    ishomd.5 $e |- H = ( hom ` T ) $.
    $( The predicate ` F e. ( ( hom `` T ) `` <. A , B >. ) ` JFM vol. 1.2
       p. 411 th. 18. $)
    ishomd $p |- ( ( T e. Cat /\ A e. O /\ B e. O ) ->
      ( F e. ( H ` <. A , B >. ) <->
        ( F e. M /\ ( D ` F ) = A /\ ( C ` F ) = B ) ) ) $=
      ( vx ccat wcel cop cfv wceq w3a wb wa wi cv csn cif cid_ cdm chom cdom_
      ccod_ fveq2 dmeqd syl5eq eleq2d anbi12d fveq1d eqeq1d 3anbi123d bibi12d
      imbi12d eqid visset 1cat elimel ishomc dedth 3impib ) EPQZAIQZBIQZFABRZGS
      ZQZFHQZFDSZATZFCSZBTZUAZUBZVJVKVLUCZWBUDAVJEOUEZWDRZWDRUFZWFRWDWERUFWEWER
      WERUFRRZUGZUHSZUIZQZBWJQZUCZFVMWHUJSZSZQZFWHUKSZUIZQZFWQSZATZFWHULSZSZBTZ
      UAZUBZUDEWGEWHTZWCWMWBXFXGVKWKVLWLXGIWJAXGEUHSZUIWJIXGXHWIEWHUHUMUNJUOZUP
      XGIWJBXIUPUQXGVOWPWAXEXGVNWOFXGVMGWNXGEUJSWNGEWHUJUMNUOURUPXGVPWSVRXAVTXD
      XGHWRFXGEUKSZUIWRHXGXJWQEWHUKUMZUNKUOUPXGVQWTAXGFDWQXGXJWQDXKLUOURUSXGVSX
      CBXGFCXBXGEULSXBCEWHULUMMUOURUSUTVAVBABXBWQWHFWNWRWJWJVCWRVCWQVCXBVCWNVCE
      WGPWDOVDVEVFVGVHVI $.
      $( [30-Nov-2007] $)
  $}

  ${
    ehm.1 $e |- O = dom ( id_ ` T ) $.
    ehm.2 $e |- M = dom ( dom_ ` T ) $.
    ehm.5 $e |- H = ( hom ` T ) $.
    $( The elements of a homset are morphisms.  JFM CAT_1 th. 21. $)
    ehm $p |- ( ( T e. Cat /\ A e. O /\ B e. O ) ->
      ( F e. ( H ` <. A , B >. ) -> F e. M ) ) $=
      ( ccat wcel w3a cop cfv wa cdom_ wceq ccod_ eqid ishomd biimpa 3simp1d
      ex ) CKLAGLBGLMZDABNEOLZDFLZUEUFPUGDCQOZOARZDCSOZOBRZUEUFUGUIUKMABUJUHCDE
      FGHIUHTUJTJUAUBUCUD $.
      $( [5-Dec-2007] $)
  $}

  ${
    dehm.1 $e |- O = dom ( id_ ` T ) $.
    dehm.2 $e |- D = ( dom_ ` T ) $.
    dehm.5 $e |- H = ( hom ` T ) $.
    $( Domain of an element of a homset.  JFM CAT_1 th. 23. $)
    dehm $p |- ( ( T e. Cat /\ A e. O /\ B e. O ) ->
      ( F e. ( H ` <. A , B >. ) -> ( D ` F ) = A ) ) $=
      ( ccat wcel w3a cop cfv wceq wa cdom_ cdm ccod_ eqid ishomd biimpa
      3simp2d ex ) DKLAGLBGLMZEABNFOLZECOAPZUFUGQEDROSZLZUHEDTOZOBPZUFUGUJUHULM
      ABUKCDEFUIGHUIUAIUKUAJUBUCUDUE $.
      $( [5-Dec-2007] $)
  $}

  ${
    cehm.1 $e |- O = dom ( id_ ` T ) $.
    cehm.2 $e |- C = ( cod_ ` T ) $.
    cehm.5 $e |- H = ( hom ` T ) $.
    $( Codomain of an element of a homset.  JFM CAT_1 th. 23. $)
    cehm $p |- ( ( T e. Cat /\ A e. O /\ B e. O ) ->
      ( F e. ( H ` <. A , B >. ) -> ( C ` F ) = B ) ) $=
      ( ccat wcel w3a cop cfv wceq wa cdom_ cdm eqid ishomd biimpa 3simp3d ex
      ) DKLAGLBGLMZEABNFOLZECOBPZUEUFQEDROZSZLZEUHOAPZUGUEUFUJUKUGMABCUHDEFUIGH
      UITUHTIJUAUBUCUD $.
      $( [5-Dec-2007] $)
  $}


  ${
    mrdmcd.1 $e |- M = dom ( dom_ ` T ) $.
    mrdmcd.2 $e |- H = ( hom ` T ) $.
    mrdmcd.3 $e |- D = ( dom_ ` T ) $.
    mrdmcd.4 $e |- C = ( cod_ ` T ) $.
    $( A morphism belongs to the homset between its domain and its codomain.
       JFM CAT_1 th. 22. $)
    mrdmcd $p |- ( T e. Cat -> ( F e. M ->
      F e. ( H ` <. ( D ` F ) , ( C ` F ) >. ) ) ) $=
      ( ccat wcel cfv cid_ cdm wa cop cded calg wi catded dedalg wf cdom_
      eqcomi dmeqi eqtri eqid doma ffvelrn ex syl 3syl coda jcad wceq w3a
      ishomd biimprcd 3expib com3l mp2an com23 mpdd ) CKLZDFLZDBMZCNMZOZLZDAMZV
      ILZPZDVGVKQEMLZVEVFVJVLVECRLZCSLZVFVJTZCUAZCUBZVPFVIBUCZVQBCVHFVIFCUDMZOB
      OGWABBWAIUEUFUGZIVIUHZVHUHZUIVTVFVJFVIDBUJUKULUMVEVOVPVFVLTZVRVSVPFVIAUCZ
      WEABCVHFVIWBIWCWDJUNWFVFVLFVIDAUJUKULUMUOVEVMVFVNVEVJVLVFVNTZVGVGUPZVKVKU
      PZVEVJVLUQZWGTVGUHVKUHVFWHWIPWJVNVFWHWIWJVNTWJVNVFWHWIUQVGVKABCDEFVIWCGIJ
      HURUSUTVAVBUTVCVD $.
      $( [1-Nov-2007] $)
  $}

  $( JFM CAT_1 th. 44.  Not included. use idosd instead $)

  ${
    eqidob.1 $e |- O = dom J $.
    eqidob.2 $e |- J = ( id_ ` C ) $.
    $( When the identities are equal, the objects are equal.  JFM CAT_1 th.
       45. $)
    eqidob $p |- ( ( C e. Cat /\ A e. O /\ B e. O ) -> ( ( J ` A ) =
    ( J ` B ) -> A = B ) ) $=
      ( ccat wcel w3a cfv cdom_ wceq cded ccod_ wa wi catded id 3anim123i eqid
      idosd 3adant3 3adant2 jca eqeq12 biimpd ad2ant2r 3syl fveq2 syl5 ) CHIZAE
      IZBEIZJZADKZCLKZKZBDKZUQKZMZABMZUPUSMUOCNIZUMUNJZURAMZUPCOKZKAMZPZUTBMZUS
      VFKBMZPZPVAVBQZULVCUMUMUNUNCRUMSUNSTVDVHVKVCUMVHUNAVFUQCDEFUQUAZGVFUAZUBU
      CVCUNVKUMBVFUQCDEFVMGVNUBUDUEVEVIVLVGVJVEVIPVAVBURAUTBUFUGUHUIUPUSUQUJUK
      $.
      $( [24-Apr-2007] $)
  $}

  ${
    homib.1 $e |- O = dom ( id_ ` T ) $.
    homib.2 $e |- J = ( id_ ` T ) $.
    homib.3 $e |- H = ( hom ` T ) $.
    $( The homset which ` ( ( id_ `` T ) `` A ) ` belongs to.  JFM CAT_1 th.
       55. $)
    homib $p |- ( ( T e. Cat /\ A e. O ) -> ( J ` A ) e.
      ( H ` <. A , A >. ) ) $=
      ( ccat wcel wa cfv cdom_ ccod_ cop cdm eqid jdmo wi mrdmcd adantr mpd
      cid_ wceq idosc eqcomi fveq1i fveq2i eqeq1i biimpi syl cded catded
      anim1i dmeqi eqtri idosd ancom sylib pm3.26 3syl opeq12d fveq2d eleqtrd
      ) BIJZAEJZKZADLZVHBMLZLZVHBNLZLZOZCLZAAOZCLVGVHVIPZJZVHVNJZABDVPEVPQZFGRV
      EVQVRSVFVKVIBVHCVPVSHVIQZVKQZTUAUBVGVMVOCVGVJAVLAVGABUCLZLZVILZAUDZWCVKLA
      UDZKVJAUDZAVKVIBWBEFVTWBQWAUEWEWGWFWEWGWDVJAWCVHVIAWBDDWBGUFZUGUHUIUJUAUK
      VGBULJZVFKZVLAUDZWGKZWKVEWIVFBUMUNWJWGWKKWLAVKVIBDEEWBPDPFWBDWHUOUPVTGWAU
      QWGWKURUSWKWGUTVAVBVCVD $.
      $( [5-Dec-2007] $)
  $}

  ${
    hine.1 $e |- O = dom ( id_ ` T ) $.
    hine.2 $e |- H = ( hom ` T ) $.
    $( Obvious.  Only here to have the complete set of Mizar theorems. $)
    $( The homset ` ( H `` <. A , A >. ) ` is not empty.  JFM CAT_1 th. 56. $)
    hine $p |- ( ( T e. Cat /\ A e. O ) ->
      ( H ` <. A , A >. ) =/= (/) ) $=
      ( ccat wcel wa cid_ cfv cop c0 wne eqid homib ne0i syl ) BGHADHIABJKZKZAA
      LCKZHUAMNABCSDESOFPUATQR $.
      $( [3-Jan-2008] $)
  $}

  ${
    cmphmia.1 $e |- O = dom ( id_ ` T ) $.
    cmphmia.2 $e |- H = ( hom ` T ) $.
    cmphmia.3 $e |- J = ( id_ ` T ) $.
    cmphmia.4 $e |- R = ( o_ ` T ) $.
    $( Composite of the member of a homset with the identity.  JFM CAT_1 th.
       57 $)
    cmphmia $p |- ( ( T e. Cat /\ A e. O /\ B e. O ) ->
      ( F e. ( H ` <. A , B >. ) -> ( ( J ` B ) R F ) = F ) ) $=
      ( ccat wcel w3a cop cfv cdom_ cdm ccod_ wceq wa co eqid ehm cehm jcad wi
      cid_ eqcomi dmeqi eqtri cmpida 3exp imp4b 3adant2 syld ) DMNZAHNZBHNZOZEA
      BPFQNZEDRQZSZNZEDTQZQBUAZUBZBGQECUCEUAZVAVBVEVGABDEFVDHIVDUDZJUEABVFDEFHI
      VFUDZJUFUGURUTVHVIUHUSURUTVEVGVIURUTVEVGVIUHBVFVCCDEGVDHVJVCUDHDUIQZSGSIV
      LGGVLKUJUKULKLVKUMUNUOUPUQ $.
      $( [5-Dec-2007] $)
  $}

  ${
    cmphmib.1 $e |- O = dom ( id_ ` T ) $.
    cmphmib.2 $e |- H = ( hom ` T ) $.
    cmphmib.3 $e |- J = ( id_ ` T ) $.
    cmphmib.4 $e |- R = ( o_ ` T ) $.
    $( Composite of a member of a homset with the identity.  JFM CAT_1 th.
       58 $)
    cmphmib $p |- ( ( T e. Cat /\ A e. O /\ B e. O ) ->
      ( F e. ( H ` <. A , B >. ) -> ( F R ( J ` A ) ) = F ) ) $=
      ( ccat wcel w3a cop cfv cdom_ cdm wceq wa co eqid ehm dehm jcad wi cid_
      eqcomi dmeqi eqtri cmpidb 3exp imp4b 3adant3 syld ) DMNZAHNZBHNZOZEABPFQN
      ZEDRQZSZNZEVBQATZUAZEAGQCUBETZUTVAVDVEABDEFVCHIVCUCZJUDABVBDEFHIVBUCZJUEU
      FUQURVFVGUGUSUQURVDVEVGUQURVDVEVGUGAVBCDEGVCHVHVIHDUHQZSGSIVJGGVJKUIUJUKK
      LULUMUNUOUP $.
      $( [5-Dec-2007] $)
  $}

  ${
    iri.1 $e |- O = dom ( id_ ` T ) $.
    iri.2 $e |- J = ( id_ ` T ) $.
    iri.3 $e |- R = ( o_ ` T ) $.
    $( Composite of an identity with itself.  JFM CAT_1 th. 59 $)
    iri $p |- ( ( T e. Cat /\ A e. O ) ->
      ( ( J ` A ) R ( J ` A ) ) = ( J ` A ) ) $=
      ( ccat wcel wa cfv cdom_ cdm ccod_ wceq co eqid jdmo cid_ eqcomi dmeqi
      eqtri idosc pm3.27d wi cmpida 3exp imp4b mp2and ) CIJZAEJZKZADLZCMLZNZJZU
      NCOLZLAPZUNUNBQUNPZACDUPEUPRZFGSUMUNUOLAPUSAURUOCDEECTLZNDNFVBDDVBGUAUBUC
      ZUORZGURRZUDUEUKULUQUSUTUKULUQUSUTUFAURUOBCUNDUPEVAVDVCGHVEUGUHUIUJ $.
      $( [5-Dec-2007] $)
  $}

  ${
    cmpassoh.1 $e |- O = dom ( id_ ` T ) $.
    cmpassoh.2 $e |- H = ( hom ` T ) $.
    cmpassoh.3 $e |- R = ( o_ ` T ) $.
    $( ` o_ ` is associative.  Homset-based version of ~ cmpasso . $)
    cmpassoh $p |- ( ( T e. Cat /\ ( A e. O /\ B e. O ) /\
      ( C e. O /\ D e. O ) ) -> ( ( L e. ( H ` <. A , B >. ) /\
         M e. ( H ` <. B , C >. ) /\ N e. ( H ` <. C , D >. ) )
           -> ( N R ( M R L ) ) = ( ( N R M ) R L ) ) ) $=
      ( ccat wcel wa w3a cop cfv co wceq cdom_ cdm ccod_ eqid cmpasso 3simpl1
      wi ehm 3expib a1dd 3imp 3exp com13 adantr com3l adantl com12 a1d
      3anim123d imp jca dehm 3simp3 syl5 cehm 3simp2 eqtr4d 3adant3r1 adantld
      3impia eqcomd ex com3r 3adant3 eqtrd 3expia 3simpa sylc ) FOPZAKPZBKPZQZC
      KPZDKPZQZRZHABSGTPZIBCSGTPZJCDSGTPZRZJIHEUAEUAJIEUAHEUAUBZWAHFUCTZUDZPZIW
      OPZJWOPZRZQJWNTZIFUETZTZUBZIWNTZHXATZUBZQWMWHWLQZXAWNEFHIJWOWOUFZWNUFZXAU
      FZNUGXGWAWSWAWDWGWLUHWHWLWSWHWIWPWJWQWKWRWAWDWGWIWPUIZWAWDXKWGWAWBWCXKABF
      HGWOKLXHMUJUKULUMWAWDWGWJWQUIZWDWAWGXLUIZWCWAXMUIWBWGWCWAXLWEWCWAXLUIUIWF
      WAWCWEXLWAWCWEXLBCFIGWOKLXHMUJUNUOUPUQURUSUMWAWDWGWKWRUIZWAWGXNUIWDWAWEWF
      XNCDFJGWOKLXHMUJUKUTUMVAVBVCXGXCXFXGWTCXBWHWLWTCUBZWHWKXOWLWAWDWGWKXOUIZW
      AWGXPUIWDWAWEWFXPCDWNFJGKLXIMVDUKUTUMWIWJWKVEVFVBWHWLXBCUBZWHWJXQWLWAWDWG
      WJXQUIZWDWAWGXRUIZWCWAXSUIWBWGWCWAXRWEWCWAXRUIUIWFWAWCWEXRWAWCWEXRBCXAFIG
      KLXJMVGUNUOUPUQURUSUMWIWJWKVHVFVBVIWHWLXFWHWIWJQZXFWLWAWDWGXTXFUIZWGWDWAY
      AWEWDWAYAUIZUIWFWDWEYBWBWCWEYBWAWBWCWERZYAWAYCXTXFWAYCXTRXDBXEWAYCXTXDBUB
      ZWAYCQWJYDWIWAWCWEWJYDUIWBBCWNFIGKLXIMVDVJVKVLWAYCXTBXEUBZYCWAXTYEUIZWBWC
      WAYFUIWEXTWAWDYEWIWAWDYEUIUIWJWAWDWIYEWAWBWCWIYEUIWAWBWCRZWIYEYGWIQXEBYGW
      IXEBUBABXAFHGKLXJMVGVBVMVNUKVOUPUOVPUSUMVQUNUSVRUSUPUOUMWIWJWKVSVFVBVCVTV
      N $.
      $( [10-Mar-2008] $)
  $}


  ${
    homgrf.1 $e |- O = dom ( id_ ` T ) $.
    homgrf.2 $e |- H = ( hom ` T ) $.
    homgrf.3 $e |- R = ( o_ ` T ) $.
    $( Homset of a composite.  JFM CAT_1 th. 51 $)
    homgrf $p |- ( ( T e. Cat /\ ( A e. O /\ B e. O /\ C e. O ) ) ->
   ( ( F e. ( H ` <. A , B >. ) /\ G e. ( H ` <. B , C >. ) ) ->
     ( G R F ) e. ( H ` <. A , C >. ) ) ) $=
      ( ccat wcel w3a wa cop cfv co cdom_ cdm wceq ccod_ wi eqid ehm 3adant3r3
      com12 adantr impcom 3adant3r1 adantl dehm cehm imp eqcomd ex eqtrd 3jca
      cmpmorp 3exp 3impd syld cded domcmpd catded ad2antrr eqtr4d sylc codcmpd
      wb ishomd 3adant3r2 sylibrd ) EMNZAINZBINZCINZOZPZFABQHRNZGBCQHRNZPZGFDSZ
      ETRZUAZNZWDWERZAUBZWDEUCRZRZCUBZOZWDACQHRNZVTWCWMVTWCPZWGWIWLVTWCWGVTWCFW
      FNZGWFNZGWERZFWJRZUBZOZWGVTWCXAWOWPWQWTWCVTWPWAVTWPUDWBVTWAWPVOVPVQWAWPUD
      VRABEFHWFIJWFUEZKUFUGUHUIUJZWCVTWQWBVTWQUDWAVTWBWQVOVQVRWBWQUDVPBCEGHWFIJ
      XBKUFUKUHULUJZWOWRBWSWCVTWRBUBZWBVTXEUDWAVTWBXEVOVQVRWBXEUDVPBCWEEGHIJWEU
      EZKUMUKUHULUJZWCVTBWSUBZWAVTXHUDWBVTWAXHVOVPVQWAXHUDVRVOVPVQOZWAXHXIWAPWS
      BXIWAWSBUBZABWJEFHIJWJUEZKUNZUOUPUQUGUHUIUJURUSUQVOXAWGUDVSVOWPWQWTWGVOWP
      WQWTWGUDWJWEDEFGWFXBXFXKLUTVAVBUIVCUOWOWHFWERZAEVDNZWPWQOZWTWHXMUBWOWJWED
      EFGWFXBXFXKLVEWOXNWPWQVOXNVSWCEVFVGXCXDUSZWOWRBWSXGWCVTXJWAVTXJUDWBVTWAXJ
      VOVPVQWAXJUDVRXLUGUHUIUJVHZVIWCVTXMAUBZWAVTXRUDWBVTWAXRVOVPVQWAXRUDVRABWE
      EFHIJXFKUMUGUHUIUJURWOWKGWJRZCXOWTWKXSUBWOWJWEDEFGWFXBXFXKLVJXPXQVIWCVTXS
      CUBZWBVTXTUDWAVTWBXTVOVQVRWBXTUDVPBCWJEGHIJXKKUNUKUHULUJURUSUQVOVPVRWNWMV
      KVQACWJWEEWDHWFIJXBXFXKKVLVMVN $.
      $( [10-Mar-2008] $)
  $}


