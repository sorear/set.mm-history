$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Finite composites ( i. e. finite sums, products ... )
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c prod3_ $. $( Product sign. $)

  $( Extend class notation to include finite products/sums. $)
  cprd $a class prod3_ k e. A G B $.

  ${
    $d m n x y k $.  $d m n x y A $.  $d m n x y G $.  $d m n x y B $.
    $( Part of the definition of ~ df-prod2 .  Don't use by itself. $)
    df-prod $a |- prod3_ k e. A G B = { x | E. m E. n e. ( ZZ>= ` m )
        ( A = ( m ... n ) /\
      x e. ( ( <. m , G >. seqz ( { <. k , y >. | y = B } |` ZZ ) ) ` n ) ) }
        $.
  $}

  $c prod_ $. $( Product sign. $)

  $( Extend class notation to include finite products/sums. $)
  cprd2 $a class prod_ k e. A G B $.

  ${
    $( Define the composite for the law ` G ` of a finite sequence of elements
       whose values are defined by the expression ` B ` and whose set of
       indices is ` A ` . ` A ` may be empty.  It may be thougt as a product
       (if ` G ` is a multiplication), a sum (if ` G ` is an addition) or
       whatever.  The variable ` k ` is normally a free variable in ` B `
       ( i.e. ` B ` can be thought of as ` B ( k ) ` ).  The definition is
       meaningful when ` A ` is a finite set of sequential integers and ` G `
       is an internal operation.  Our definition corresponds to the first part
       of the definition of ~ df-sum .  The operation ` + ` has been replaced
       by the generic operation ` G ` .  The reference to the concept of limit
       has been removed because one wants to use the product in contexts where
       limits are irrelevant.  I could be still more generic and replace
       ` ( m ... n ) ` by a finite totally ordered set.  I would then get the
       definition given by Bourbaki in the first chapter of the algebra book of
       his treatise ( A I.3 def.4 ).  I don't because the present definition is
       easier to deal with and because there exists an order isomorphism
       between any finite totally ordered set and any finite sets of integers.
       I don't specify anything about ` G ` because nothing is required of
       ` g ` in the definition of ` seqz ` .  I hope it will be ok.  Otherwise
       one could add ` G e. Magma ` . $)
    df-prod2 $a |- prod_ k e. A G B =
       if ( A = (/) , ( Id ` G ) , prod3_ k e. A G B ) $.
  $}

  ${
    $d A m n x y $.  $d B m n x y $.  $d G m n x y $.  $d k m n x y $.
    $( A finite composite is a set.  (Contributed by FL, 19-Sep-2011.) $)
    prodex $p |- prod_ k e. A G B e. _V $=
      ( vm vn vx vy wceq cgi cfv cvv fvex cv co cz wa wrex cab eqeltri cprd2 c0
      cprd cif df-prod2 cfz cop copab cres cseqz cuz wex df-prod cle wbr 2rexuz
      wcel abbii zex anass ancom bitr3i ssab2 eqsstri ssexi abrexex2 ifex ) ABC
      DUAAUBIZDJKZABCDUCZUDLABCDUEVHVIVJDJMVJAENZFNZUFOIZGNVLVKDUGHNBICHUHPUIUJ
      OZKZUQZQZFVKUKKREULZGSZLGHABCEFDUMVSVKVLUNUOZVQQZFPRZEPRZGSLVRWCGVQEFUPUR
      WBEGPUSWAFGPUSWAGSZVOVLVNMWDVPVTVMQZQZGSVOWAWFGWAWEVPQWFVTVMVPUTWEVPVAVBU
      RWEGVOVCVDVEVFVFTTVGT $.
      $( [19-Sep-2011] $)
  $}

  $( The value of ` prod_ k e. (/) G B ` .  (Contributed by FL,
     19-Sep-2011.) $)
  valproemset $p |- prod_ k e. (/) G B = ( Id ` G ) $=
    ( c0 cprd2 wceq cgi cfv cprd cif df-prod2 eqid iftrue ax-mp eqtri ) DABCEDD
    FZCGHZDABCIZJZQDABCKPSQFDLPQRMNO $.
    $( [19-Sep-2011] $)

  ${
    $d A m n x y $.  $d B m n x y $.  $d C m n x y $.  $d G m n x y $.
    $d k m n x y $.
    $( Equality theorem for a composite.  (Contributed by FL, 19-Sep-2011.) $)
    prodeq1 $p |- ( A = B -> prod_ k e. A G C = prod_ k e. B G C ) $=
      ( vm vn vx vy wceq c0 cfv cprd cif cprd2 eqeq1 cv co wa wrex cgi copab cz
      cfz cop cres cseqz wcel cuz wex cab anbi1d rexbidv exbidv df-prod 3eqtr4g
      abbidv ifbieq2d df-prod2 ) ABJZAKJZEUALZACDEMZNBKJZVBBCDEMZNACDEOBCDEOUTV
      AVDVCVEVBABKPUTAFQZGQZUDRZJZHQVGVFEUEIQCJDIUBUCUFUGRLUHZSZGVFUILZTZFUJZHU
      KBVHJZVJSZGVLTZFUJZHUKVCVEUTVNVRHUTVMVQFUTVKVPGVLUTVIVOVJABVHPULUMUNUQHIA
      CDFGEUOHIBCDFGEUOUPURACDEUSBCDEUSUP $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A k m n x y $.  $d B m n x y $.  $d G k m n x y $.
    hbprod1.1 $e |- ( x e. A -> A. k x e. A ) $.
    $( Bound-variable hypothesis builder for ` prod_ ` .  (Contributed by FL,
       14-Sep-2010.) $)
    hbprod1 $p |- ( x e. prod_ k e. A G B ->
      A. k x e. prod_ k e. A G B ) $=
      ( vm vn vy cprd2 c0 wceq cfv ax-17 cv wcel co cz cseqz hbxfreq cprd copab
      cgi cif df-prod2 cfz cop cres wa cuz wrex wex df-prod hbopab1 hbres hbopr
      cab wel hbfv hban hbrex hbex hbab hbif ) DABCDEJBKLZEUCMZBCDEUAZUDBCDEUEV
      EDAVFVGVEDNAOZVFPDNDAVGBGOZHOZUFQLZVHVJVIEUGZIOCLZDIUBZRUHZSQZMPZUIZHVIUJ
      MZUKZGULZAUQAIBCDGHEUMWADAAVTDGVRDHVSVJVSPDNVKVQDVKDNDAVJVPDAVLVOSVHVLPDN
      VHSPDNDAVNRVMDIAUNVHRPDNUOUPAHURDNUSUTVAVBVCTVDT $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d A m n w x y z $.  $d B m n w y z $.  $d G m n w x y z $.
    $d k m n w x y z $.
    hbprod.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbprod.2 $e |- ( y e. G -> A. x y e. G ) $.
    hbprod.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for ` prod_ ` .  If ` x ` is
       (effectively) not free in ` A ` , ` G ` and ` B ` , it is not free in
       ` prod_ k e. A G B ` .  (Contributed by FL, 19-Sep-2011.) $)
    hbprod $p |- ( y e. prod_ k e. A G B -> A. x y e. prod_ k e. A G B ) $=
      ( vm vn vz vw wceq cfv ax-17 cv wcel cz cseqz cprd2 cgi cprd cif df-prod2
      c0 cfz co cop copab cres wa cuz wrex wex cab df-prod hbeq wal eleq1 albid
      wi weq imbi12d hbop chvar hbopab hbres hbopr hbfv hban hbrex hbex hbxfreq
      wel hbab hbif ) ABCDEFUACUFNZFUBOZCDEFUCZUDCDEFUEVRABVSVTVRAPBQZVSRAPABVT
      CJQZKQZUGUHZNZLQZWCWBFUIZMQZDNZEMUJZSUKZTUHZORZULZKWBUMOZUNZJUOZLUPLMCDEJ
      KFUQWQALBWPAJWNAKWOWCWORAPWEWMAABBCWDGWAWDRAPURALWCWLALWGWKTWAWGRZWRAUSZV
      BWFWGRZWTAUSZVBZBLXBBPBLVCZWRWTWSXAWAWFWGUTZXCWRWTAXCAPXDVAVDABWBFBJVOAPH
      VEVFWFTRAPALWJSWIEMALABBWHDBMVOAPIURVGWFSRAPVHVILKVOAPVJVKVLVMVPVNVQVN $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A k m n x y $.  $d B m n x y $.  $d C m n x y $.  $d G m n x y $.
    prodeq2.1 $e |- G e. D $.
    $( Equality theorem for a composite.  (Contributed by FL, 19-Sep-2011.) $)
    prodeq2 $p |- ( A. k e. A B = C ->
      prod_ k e. A G B = prod_ k e. A G C ) $=
      ( vm vn vx vy wceq cfv cv co cz cres wcel wa cvv wral c0 cgi cprd cif cfz
      cprd2 cop copab cseqz cuz wrex wex cab hbra1 ax-17 ra4 imp eqeq2d opabbid
      resopab 3eqtr4g adantr reseq2 adantl 3eqtr3d opreq2d fveq1d adantlr fvres
      pm5.32da elfzelz syl eqtr4d rgen elisseti funopabeq resfunexg mp2an oprex
      zex seqzfveq mpan2 ad2antlr 3eqtr4d eleq2d rexbidva exbidv abbidv df-prod
      wfun ifeq2d df-prod2 ) BCLZEAUAZAUBLZFUCMZABEFUDZUEWPWQACEFUDZUEABEFUGACE
      FUGWOWPWRWSWQWOAHNZINZUFOZLZJNZXAWTFUHZKNZBLZEKUIZPQZUJOMZRZSZIWTUKMZULZH
      UMZJUNXCXDXAXEXFCLZEKUIZPQZUJOMZRZSZIXMULZHUMZJUNWRWSWOXOYCJWOXNYBHWOXLYA
      IXMWOXAXMRZSZXCXKXTYEXCSZXJXSXDYFXAXEXHXBQZUJOZMZXAXEXQXBQZUJOZMZXJXSWOXC
      YIYLLYDWOXCSZXAYHYKYMYGYJXEUJYMXHAQZXQAQZYGYJWOYNYOLXCWOENARZXGSZEKUIYPXP
      SZEKUIYNYOWOYQYREKWNEAUOWOKUPWOYPXGXPWOYPSBCXFWOYPWNWNEAUQURUSVKUTXGEKAVA
      XPEKAVAVBVCXCYNYGLWOAXBXHVDVEXCYOYJLWOAXBXQVDVEVFVGVHVIYDXJYILZWOXCYDXDXI
      MZXDYGMZLZJXBUAYSUUBJXBXDXBRZYTXDXHMZUUAUUCXDPRZYTUUDLXDWTXAVLZXDPXHVJVMX
      DXBXHVJVNVOFJXIYGWTXAFDGVPZXHWKZPTRZXITREKBVQZWAXHPTVRVSUUHXBTRZYGTRUUJWT
      XAUFVTZXHXBTVRVSWBWCWDYDXSYLLZWOXCYDXDXRMZXDYJMZLZJXBUAUUMUUPJXBUUCUUNXDX
      QMZUUOUUCUUEUUNUUQLUUFXDPXQVJVMXDXBXQVJVNVOFJXRYJWTXAUUGXQWKZUUIXRTREKCVQ
      ZWAXQPTVRVSUURUUKYJTRUUSUULXQXBTVRVSWBWCWDWEWFVKWGWHWIJKABEHIFWJJKACEHIFW
      JVBWLABEFWMACEFWMVB $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A m n x y $.  $d B m n x y $.  $d C m n x y $.  $d G m n x y $.
    $d j m n x y $.  $d k m n x y $.
    cbvprod.1 $e |- ( x e. B -> A. k x e. B ) $.
    cbvprod.2 $e |- ( x e. C -> A. j x e. C ) $.
    cbvprod.3 $e |- ( j = k -> B = C ) $.
    $( Change bound variable in a finite composite.  (Contributed by FL,
       19-Sep-2011.) $)
    cbvprodi $p |- prod_ j e. A G B = prod_ k e. A G C $=
      ( vm vn vy wceq cfv cprd cv co cz cseqz cgi cif cprd2 biid cfz copab cres
      c0 cop wcel wa cuz wrex wex cab hbeleq weq eqeq2d cbvopab1 reseq1 opreq2i
      ax-mp fveq1i eleq2i anbi2i rexbii exbii df-prod 3eqtr4i ifbieq2i df-prod2
      abbii ) BUHNZGUAOZBCEGPZUBVMVNBDFGPZUBBCEGUCBDFGUCVMVMVOVPVNVMUDBKQZLQZUE
      RNZMQZVRVQGUIZAQZCNZEAUFZSUGZTRZOZUJZUKZLVQULOZUMZKUNZMUOVSVTVRWAWBDNZFAU
      FZSUGZTRZOZUJZUKZLWJUMZKUNZMUOVOVPWLXAMWKWTKWIWSLWJWHWRVSWGWQVTVRWFWPWEWO
      WATWDWNNWEWONWCWMEAFFACHUPEADIUPEFUQCDWBJURUSWDWNSUTVBVAVCVDVEVFVGVLMABCE
      KLGVHMABDFKLGVHVIVJBCEGVKBDFGVKVI $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A m n x y $.  $d C m n x y $.  $d G m n x y $.  $d H m n x y $.
    $d k m n x y $.
    $( Equality theorem for a composite.  (Contributed by FL, 19-Sep-2011.) $)
    prodeq3 $p |- ( G = H -> prod_ k e. A G C = prod_ k e. A H C ) $=
      ( vm vn vx vy wceq cgi cfv cprd cif cprd2 cv co cop cseqz wcel fveq2 cres
      c0 cfz copab cz wa cuz wex cab opeq2 opreq1d fveq1d eleq2d anbi2d rexbidv
      wrex exbidv abbidv df-prod 3eqtr4g ifeq12 syl11anc df-prod2 ) DEJZAUCJZDK
      LZABCDMZNZVFEKLZABCEMZNZABCDOABCEOVEVGVJJVHVKJVIVLJDEKUAVEAFPZGPZUDQJZHPZ
      VNVMDRZIPBJCIUEUFUBZSQZLZTZUGZGVMUHLZUQZFUIZHUJVOVPVNVMERZVRSQZLZTZUGZGWC
      UQZFUIZHUJVHVKVEWEWLHVEWDWKFVEWBWJGWCVEWAWIVOVEVTWHVPVEVNVSWGVEVQWFVRSDEV
      MUKULUMUNUOUPURUSHIABCFGDUTHIABCFGEUTVAVFVGVJVHVKVBVCABCDVDABCEVDVA $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A k $.
    prodeq123i.1 $e |- A = B $.
    prodeq123i.2 $e |- G = H $.
    prodeq123i.3 $e |- ( k e. A -> C = D ) $.
    prodeq123i.4 $e |- G e. F $.
    $( Conditions for two composites to be equal.  (Contributed by FL,
       6-Sep-2010.) $)
    prodeq123i $p |- prod_ k e. A G C = prod_ k e. B H D $=
      ( wceq cprd2 wral prodeq2 prodeq1 ax-mp prodeq3 eqtri syl6eq mprg ) CDMZA
      CEGNZBDEHNZMEAUCEAOUDADEGNZUEACDFEGLPUFBDEGNZUEABMUFUGMIABDEGQRGHMUGUEMJB
      DEGHSRTUAKUB $.
      $( [6-Sep-2010] $)
  $}

  ${
    $d A k $.
    prodeq123d.1 $e |- ( ph -> A = B ) $.
    prodeq123d.2 $e |- ( ph -> G = H ) $.
    prodeq123d.3 $e |- ( ph -> A. k e. A C = D ) $.
    prodeq123d.4 $e |- G e. F $.
    $( Conditions for two composites to be equal.  (Contributed by FL,
       5-Sep-2010.) $)
    prodeq123d $p |- ( ph -> prod_ k e. A G C = prod_ k e. B H D ) $=
      ( cprd2 wceq wral prodeq2 syl prodeq3 prodeq1 3eqtrd ) ABDFHNZBEFHNZBEFIN
      ZCEFINZADEOFBPUBUCOLBDEGFHMQRAHIOUCUDOKBEFHISRABCOUDUEOJBCEFITRUA $.
      $( [5-Sep-2010] $)
  $}

  ${
    prodeq1d.1 $e |- ( ph -> A = B ) $.
    $( Conditions for two composites to be equal.  (Contributed by FL,
       5-Sep-2010.) $)
    prodeq1d $p |- ( ph -> prod_ k e. A G C = prod_ k e. B G C ) $=
      ( wceq cprd2 prodeq1 syl ) ABCHBDEFICDEFIHGBCDEFJK $.
      $( [5-Sep-2010] $)
  $}

  ${
    prodeq2d.2 $e |- ( ph -> G = H ) $.
    $( Conditions for two composites to be equal.  (Contributed by FL,
       5-Sep-2010.) $)
    prodeq2d $p |- ( ph -> prod_ k e. A G C = prod_ k e. A H C ) $=
      ( wceq cprd2 prodeq3 syl ) AEFHBCDEIBCDFIHGBCDEFJK $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A k $.
    prodeq3d.3 $e |- ( ph -> A. k e. A C = D ) $.
    prodeq3d.4 $e |- G e. F $.
    $( Conditions for two composites to be equal.  (Contributed by FL,
       5-Sep-2010.) $)
    prodeq3d $p |- ( ph -> prod_ k e. A G C = prod_ k e. A G D ) $=
      ( wceq wral cprd2 prodeq2 syl ) ACDJEBKBCEGLBDEGLJHBCDFEGIMN $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A y $.  $d B k y $.  $d C k $.
    prodeqfv.1 $e |- A e. D $.
    prodeqfv.2 $e |- F = { <. k , y >. | ( k e. B /\ y = A ) } $.
    prodeqfv.3 $e |- G e. F $.
    $( Convert a composite of function values to a composite of classes
       ` A ( k ) ` .  (Contributed by FL, 19-Sep-2011.) $)
    prodeqfv $p |- ( C C_ B
     -> prod_ k e. C G ( F ` k ) = prod_ k e. C G A ) $=
      ( wss cv cfv wceq wcel wa ssel2 copab fveq1i fvopab2 mpan2 syl r19.21aiva
      syl5eq prodeq3d ) DCLZDFMZGNZBFGHUGUIBOZFDUGUHDPQUHCPZUJDCUHRUKUIUHUKAMBO
      QFASZNZBUHGULJTUKBEPUMBOIFACBEUAUBUEUCUDKUF $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A m n x y $.  $d G m n x y $.  $d M m n x y $.  $d N m n x y $.
    $d k m n x y $.
    $( Special case of composite over a finite index set.  (Contributed by FL,
       5-Sep-2010.) $)
    dffprod $p |- ( N e. ( ZZ>= ` M ) -> prod_ k e. ( M ... N ) G A =
     ( ( <. M , G >. seqz ( { <. k , y >. | y = A } |` ZZ ) ) ` N ) ) $=
      ( vm vn vx cfv wcel co wceq cv cz cle wbr cxr wa wrex cuz cfz c0 cgi cprd
      cprd2 cif cop copab cres cseqz df-prod2 wi eluzel2 eluzelz eluzle w3a clt
      wn simp3 wb cr zssre ressxr sstri sseli anim12i 3adant3 xrlenlt syl mpbid
      fzn notbid iffalse wex cab df-prod cvv visset fzopth mpan2 anbi12i syl6bb
      eqcom anbi1d rexbidv exbidv 2rexuz an12 2rexbii bitri breq1 opeq1 opreq1d
      fveq1d eleq2d anbi12d breq2 fveq2 ceqsrex2v biantrurd bitr4d syl5bb bitrd
      syl11anc abbi1dv syl5eq eqeq1 syl5ibr syl111anc pm2.43i ) FEUAJKZEFUBLZBC
      DUFXMUCMZDUDJZXMBCDUEZUGZFEDUHZANBMCAUIOUJZUKLZJZXMBCDULXLXQYAMZXLEOKZFOK
      ZEFPQZXLYBUMZEFUNZEFUOZEFUPZYCYDYEUQZXNUSZYFYJFEURQZUSZYKYJYEYMYCYDYEUTYJ
      ERKZFRKZSZYEYMVAYCYDYPYEYCYNYDYOOREOVBRVCVDVEZVFORFYQVFVGVHEFVIVJVKYCYDYM
      YKVAYEYCYDSYLXNEFVLVMVHVKYKXQXPMZYFXNXOXPVNXLYBYRXPYAMXLXPXMGNZHNZUBLMZIN
      ZYTYSDUHZXSUKLZJZKZSZHYSUAJZTZGVOZIVPYAIAXMBCGHDVQXLUUJIYAXLUUJYSEMZYTFMZ
      SZUUFSZHUUHTZGVOZUUBYAKZXLUUIUUOGXLUUGUUNHUUHXLUUAUUMUUFXLUUAEYSMZFYTMZSZ
      UUMXLYTVRKUUAUUTVAHVSVRYSYTEFVTWAUURUUKUUSUULEYSWDFYTWDWBWCWEWFWGUUPUUMYS
      YTPQZUUFSZSZHOTGOTZXLUUQUUPUVAUUNSZHOTGOTUVDUUNGHWHUVEUVCGHOOUVAUUMUUFWIW
      JWKXLUVDYEUUQSZUUQXLYCYDUVDUVFVAYGYHUVBEYTPQZUUBYTXTJZKZSUVFGHEFOOUUKUVAU
      VGUUFUVIYSEYTPWLUUKUUEUVHUUBUUKYTUUDXTUUKUUCXRXSUKYSEDWMWNWOWPWQUULUVGYEU
      VIUUQYTFEPWRUULUVHYAUUBYTFXTWSWPWQWTXEXLYEUUQYIXAXBXCXDXFXGXQXPYAXHXIVJVJ
      XJXKXG $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d F k y $.  $d G y $.  $d M y $.  $d N y $.
    fprodserz.1 $e |- F e. A $.
    fprodserz.2 $e |- G e. B $.
    $( A finite composite expressed in terms of a partial composite of an
       infinite series.  (Contributed by FL, 5-Sep-2010.) $)
    fprodserz $p |- ( N e. ( ZZ>= ` M ) ->
      prod_ k e. ( M ... N ) G ( F ` k ) = ( ( <. M , G >. seqz F ) ` N ) ) $=
      ( vy cuz cfv wcel cfz co cv wceq cz cseqz elisseti cprd2 cop cres dffprod
      copab eluzel2 seqzres2 syl fveq1d eqtrd ) GFKLMZFGNOCPDLZCEUAGFEUBZJPULQC
      JUERUCSOZLGUMDSOZLJULCEFGUDUKGUNUOUKFRMUNUOQFGUFJECDFEBITDAHTUGUHUIUJ $.
      $( [5-Sep-2010] $)

  $}

  ${
    $d F j x $.  $d G x $.  $d M x $.  $d N x $.  $d j k x $.
    fprodserzfi.1 $e |- F e. A $.
    fprodserzfi.2 $e |- G e. B $.
    fprodserzfi.3 $e |- ( x e. F -> A. k x e. F ) $.
    $( Version of ~ fprodserz with a bound-variable hypothesis instead of a
       distinct variable condition.  (Contributed by FL, 5-Sep-2010.) $)
    fprodserzfi $p |- ( N e. ( ZZ>= ` M ) -> prod_ k e. ( M ... N ) G ( F ` k )
                   = ( ( <. M , G >. seqz F ) ` N ) ) $=
      ( vj cuz cfv wcel cfz co cv cprd2 ax-17 cop cseqz wel hbfv fveq2 cbvprodi
      fprodserz syl5eq ) HGMNOGHPQZDRZENZDFSUILRZENZLFSHGFUAEUBQNAUIUKUMDLFARUK
      OLTDAULEKALUCDTUDUJULEUEUFBCLEFGHIJUGUH $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A x y $.  $d B k y $.  $d G x $.  $d M k x y $.
    fprod1.1 $e |- ( k = M -> A = B ) $.
    fprod1.2 $e |- G e. D $.
    $( The finite composite of ` A ( k ) ` from ` k = M ` to ` M ` (i.e. a
       composite with only one term) is ` B ` i.e. ` A ( M ) ` .  (Contributed
       by FL, 5-Sep-2010.) $)
    fprod1i $p |- ( ( B e. C /\ M e. ZZ ) ->
                   prod_ k e. ( M ... M ) G A = B ) $=
      ( vy vx wcel cz wa co cprd2 cv wceq cfv cvv cfz cop copab elisset elfz1eq
      cseqz wral eleq1d biimprcd syl5 r19.21aiv wi elfzelz fvopab2 syl prodeq3d
      ex ralimia 3syl cuz uzid zex opabex2 fprodserzfi sylan9req elisseti seqz1
      hbopab1 adantl eqid fvopab4g ancoms 3eqtrd ) BCLZGMLZNGGUAOZAEFPZGGFUBEQZ
      MLZJQARNZEJUCZUFOSZGWASZBVNVOVQVPVRWASZEFPZWBVNBTLZATLZEVPUGZWEVQRBCUDWFW
      GEVPVRVPLZVRGRZWFWGVRGUEWJWGWFWJABTHUHUIUJUKWHVPWDAEDFWGWDARZEVPWIVSWGWKU
      LVRGGUMVSWGWKEJMATUNUQUOURIUPUSVOGGUTSLWEWBRGVAKTDEWAFGGEJMAVBVCZIVTEJKVH
      VDUOVEVOWBWCRVNFWAGFDIVFWLVGVIVOVNWCBREJGABMCWAHWAVJVKVLVM $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A x y $.  $d B k y $.  $d G x $.  $d M k x $.  $d N k x y $.
    fprodp1i.1 $e |- A e. C $.
    fprodp1i.2 $e |- B e. D $.
    fprodp1i.3 $e |- G e. F $.
    fprodp1i.4 $e |- ( k = ( N + 1 ) -> A = B ) $.
    $( The composite of the next term in a finite composite of ` A ( k ) ` is
       the previous term composed with ` A ( N + 1 ) = B ` .  (Contributed by
       FL, 5-Sep-2010.) $)
    fprodp1i $p |- ( N e. ( ZZ>= ` M ) -> prod_ k e. ( M ... ( N + 1 ) ) G A =
                ( prod_ k e. ( M ... N ) G A G B ) ) $=
      ( vy cfv wcel co cprd2 cz wceq vx cuz c1 caddc cfz cv copab cseqz prodeq2
      cop wa elfzelz fvopab2 sylancl mprg peano2uz cvv opabex2 elisseti hbopab1
      zex fprodserzfi syl5eqr seqzp1 syl5reqr eluzelz peano2z eqid fvopab4 3syl
      syl opreq12d 3eqtrd ) IHUBOZPZHIUCUDQZUEQZAEGRZVPHGUJEUFZSPZNUFATUKZENUGZ
      UHQZOZIWCOZVPWBOZGQHIUEQZAEGRZBGQVOVRVQVSWBOZEGRZWDWIATZWJVRTEVQVQWIAFEGL
      UIVSVQPVTACPZWKVSHVPULJENSACUMZUNUOVOVPVNPWJWDTHIUPUAUQUQEWBGHVPENSAVAURZ
      GFLUSZWAENUAUTZVBVKVCGWBHIWOWNVDVOWEWHWFBGVOWHWGWIEGRZWEWKWQWHTEWGWGWIAFE
      GLUIVSWGPVTWLWKVSHIULJWMUNUOUAUQFEWBGHIWNLWPVBVEVOISPVPSPWFBTHIVFIVGENVPA
      BSWBMWBVHBDKUSVIVJVLVM $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A j y $.  $d B j x $.  $d G y $.  $d M j k x $.  $d M j k y $.
    fprod1fi.2 $e |- G e. D $.
    fprod1fi.3 $e |- ( x e. B -> A. k x e. B ) $.
    fprod1fi.4 $e |- ( k = M -> A = B ) $.
    $( The finite composite of a term ` A ( k ) ` from ` M ` to ` M ` (i.e. a
       composite with only one term) is ` A ( M ) = B ` , where ` k ` is
       effectively not free in ` B ` .  (Contributed by FL, 5-Sep-2010.) $)
    fprod1fi $p |- ( ( B e. C /\ M e. ZZ )
       -> prod_ k e. ( M ... M ) G A = B ) $=
      ( vj vy wcel cz wa cfz cprd2 cv ax-17 csb visset csbeq1a cbvprodi csbhypf
      co wel hbcsb1 fprod1i syl5eq ) CDNHONPHHQUFZBFGRUKFLSZBUAZLGRCMUKBUMFLGMS
      BNLTFMULBLUBMLUGFTUHFULBUCUDUMCDELGHFLAHBCASHNFTJKUEIUIUJ $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A x y $.  $d G x $.  $d M x $.  $d k x y $.
    fprod1slem.1 $e |- A e. B $.
    fprod1slem.2 $e |- G e. D $.
    $( Lemma for ~ fsum1s . $)
    fprod1slem $p |- ( M e. ZZ ->
                    prod_ k e. ( M ... M ) G A = [_ M / k ]_ A ) $=
      ( vx vy cv cfz co cprd2 csb wceq cz opreq12 cvv wcel anidms csbeq1 visset
      prodeq1d eqeq12d elisseti csbex wel ax-17 hbcsb1 csbeq1a fprod1fi vtoclga
      mpan ) IKZUOLMZADENZDUOAOZPZFFLMZADENZDFAOZPIFQUOFPZUQVAURVBVCUPUTADEVCUP
      UTPUOFUOFLRUAUDDUOFAUBUEURSTUOQTUSDUOAIUCZABGUFUGJAURSCDEUOHDJUOAVDJIUHDU
      IUJDUOAUKULUNUM $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A x $.  $d M k $.  $d k y $.
    fprod1s.1 $e |- G e. D $.
    $( The finite composite of a sequence ` A ( k ) ` from ` M ` to ` M ` (i.e.
       a composite with only one term) is ` A ( M ) ` .  (Contributed by FL,
       5-Sep-2010.) $)
    fprod1s $p |- ( ( M e. ZZ /\ A. k e. ( M ... M ) A e. B ) ->
                    prod_ k e. ( M ... M ) G A = [_ M / k ]_ A ) $=
      ( vx vy wcel cfz wral cz cvv cprd2 csb wceq ralimi wsbc wi crab class2set
      co elisset fprod1slem adantr class2seteq prodeq3d adantl fz1sbc weq equid
      wa a1i sbcimdv mpan sylbid sbceqg sylibd imp 3eqtr3d sylan2 ) ABJZDFFKUCZ
      LFMJZANJZDVDLZVDADEOZDFAPZQVCVFDVDABUDRVEVGUMVDVFHAUAZDEOZDFVJPZVHVIVEVKV
      LQVGVJNCDEFHAUBGUEUFVGVKVHQVEVGVDVJADCEVFVJAQZDVDHANUGZRGUHUIVEVGVLVIQZVE
      VGVMDFSZVOVEVGVFDFSZVPVFDFUJIIUKZVEVQVPTIULVRVFVMDFMVFVMTVRVNUNUOUPUQDFVJ
      AMURUSUTVAVB $.
      $( [5-Sep-2010] $)

    ${
      $d M k $.
      $( The finite composite of a sequence ` A ( k ) ` from ` M ` to ` M `
         (i.e. a composite with only one term) is ` A ( M ) ` .  (Contributed
         by FL, 5-Sep-2010.) $)
      fprod1s2 $p |- ( ( M e. ZZ /\ [_ M / k ]_ A e. B ) ->
                    prod_ k e. ( M ... M ) G A = [_ M / k ]_ A ) $=
        ( cz wcel csb cvv cfz co wral cprd2 wceq elisset wsbc fz1sbc sbcel1g
        bitrd syl5ibr imp fprod1s syldan ) FHIZDFAJZBIZAKIZDFFLMZNZUJADEOUGPUFU
        HUKUHUKUFUGKIZUGBQUFUKUIDFRULUIDFSDFAKHTUAUBUCAKCDEFGUDUE $.
        $( [5-Sep-2010] $)
    $}
  $}

  ${
    $d M k $.
    $( The finite composite of a sequence ` A ( k ) ` from ` M ` to ` M ` (i.e.
       a composite with only one term) is ` A ( M ) ` .  (Contributed by FL,
       5-Sep-2010.) $)
    fprod1s1 $p |- ( ( G e. D /\ M e. ZZ /\ A. k e. ( M ... M ) A e. B ) ->
                    prod_ k e. ( M ... M ) G A = [_ M / k ]_ A ) $=
      ( wcel cz cfz co wral cprd2 csb wceq cvv wi elisset caddc cif imbi2d 3imp
      prodeq3 eqeq1d addex elimel fprod1s ex dedth syl ) ECGZFHGZABGDFFIJZKZULA
      DELZDFAMZNZUJEOGZUKUMUPPZPZECQUQUSUKUMULADUQERSZLZUONZPZPEREUTNZURVCUKVDU
      PVBUMVDUNVAUOULADEUTUBUCTTUKUMVBABODUTFEROUDUEUFUGUHUIUA $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A j z $.  $d B j z $.  $d B x $.  $d G z $.  $d M j z $.  $d N j k z $.
    fprodp1fi.1 $e |- A e. C $.
    fprodp1fi.2 $e |- B e. D $.
    fprodp1fi.3 $e |- ( x e. B -> A. k x e. B ) $.
    fprodp1fi.4 $e |- ( k = ( N + 1 ) -> A = B ) $.
    fprodp1fi.5 $e |- G e. F $.
    $( The composite of the next term in a finite composite of ` A ( k ) ` is
       the previous term composed with ` A ( N + 1 ) = B ` .  (Contributed by
       FL, 5-Sep-2010.) $)
    fprodp1fi $p |- ( N e. ( ZZ>= ` M ) -> prod_ k e. ( M ... ( N + 1 ) ) G A =
                ( prod_ k e. ( M ... N ) G A G B ) ) $=
      ( vj vz wcel co cprd2 cuz cfv c1 caddc cfz cv visset elisseti csbex ax-17
      csb cvv hblem csbhypf fprodp1i hbcsb1 csbeq1a cbvprodi opreq1i 3eqtr4g
      wel ) JIUAUBRIJUCUDSZUESZFPUFZBUKZPHTIJUESZVEPHTZCHSVCBFHTVFBFHTZCHSVECUL
      EPGHIJFVDBPUGZBDKUHUILOFPQVBBCQUFZVBRFUJFAQCMUMNUNUOQVCBVEFPHVJBRPUJZFQVD
      BVIQPVAFUJUPZFVDBUQZURVHVGCHQVFBVEFPHVKVLVMURUSUT $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A j x $.  $d G j $.  $d M j $.  $d N j $.  $d j k x $.
    fprodp1slem.1 $e |- A e. B $.
    fprodp1slem.2 $e |- G e. D $.
    $( Lemma for ~ fprodp1s . $)
    fprodp1slem $p |- ( N e. ( ZZ>= ` M ) -> prod_ k e. ( M ... ( N + 1 ) ) G A
           = ( prod_ k e. ( M ... N ) G A G [_ ( N + 1 ) / k ]_ A ) ) $=
      ( vj vx cv c1 caddc co cfz cprd2 csb wceq prodeq1d cuz cfv opreq1 opreq2d
      opreq2 csbeq1d opreq12d eqeq12d oprex elisseti csbex ax-17 hbcsb1 csbeq1a
      cvv wcel fprodp1fi vtoclga ) FJLZMNOZPOZADEQZFUSPOZADEQZDUTARZEOZSFGMNOZP
      OZADEQZFGPOZADEQZDVGARZEOZSJGFUAUBUSGSZVBVIVFVMVNVAVHADEVNUTVGFPUSGMNUCZU
      DTVNVDVKVEVLEVNVCVJADEUSGFPUETVNDUTVGAVOUFUGUHKAVEBUODCEFUSHDUTAUSMNUIZAB
      HUJUKDKUTAVPKLUTUPDULUMDUTAUNIUQUR $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d A x $.  $d M k $.  $d N k $.  $d k x $.
    fprodp1s.1 $e |- G e. D $.
    $( The composite of the next term in a finite sum of ` A ( k ) ` is the
       previous term composed with ` A ( N + 1 ) ` .  (Contributed by FL,
       19-Sep-2011.) $)
    fprodp1s $p |- ( ( N e. ( ZZ>= ` M )
               /\ A. k e. ( M ... ( N + 1 ) ) A e. B )
            -> prod_ k e. ( M ... ( N + 1 ) ) G A =
                ( prod_ k e. ( M ... N ) G A G [_ ( N + 1 ) / k ]_ A ) ) $=
      ( vx wcel c1 caddc co cfz wral cvv cprd2 csb wceq ralimi cuz elisset crab
      cfv wa class2set fprodp1slem adantr class2seteq prodeq3d adantl cv fzssp1
      sseld wi a1i imim12d ralimdv2 imp wsbc peano2uz eluzfz2 syl ra4sbca sylan
      equid oprex sbcimdv mp2an wb sbceqg ax-mp sylib opreq12d 3eqtr3d sylan2 )
      ABJZDFGKLMZNMZOGFUAUDZJZAPJZDVSOZVSADEQZFGNMZADEQZDVRARZEMZSVQWBDVSABUBTW
      AWCUEZVSWBIAUCZDEQZWEWJDEQZDVRWJRZEMZWDWHWAWKWNSWCWJPCDEFGIAUFHUGUHWCWKWD
      SWAWCVSWJADCEWBWJASZDVSIAPUIZTHUJUKWIWLWFWMWGEWIWEWJADCEWAWCWODWEOWAWBWOD
      VSWEWADULZWEJWQVSJWBWOWAWEVSWQFGVTUMUNWBWOUOZWAWPUPUQURUSHUJWIWODVRUTZWMW
      GSZWIWBDVRUTZWSWAVRVSJZWCXAWAVRVTJXBFGVAFVRVBVCWBDVRVSVDVEIULZXCSZVRPJZXA
      WSUOIVFGKLVGZXDWBWODVRPWRXDWPUPVHVIVCXEWSWTVJXFDVRWJAPVKVLVMVNVOVP $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d M k $.  $d N k $.
    $( The composite of the next term in a finite composite of ` A ( k ) ` is
       the previous term composed with ` A ( N + 1 ) ` .  ( Closed form of
       ~ fprodp1s .)  (Contributed by FL, 5-Sep-2010.) $)
    fprodp1s1 $p |- ( ( G e. D /\ N e. ( ZZ>= ` M ) /\
        A. k e. ( M ... ( N + 1 ) ) A e. B )
            -> prod_ k e. ( M ... ( N + 1 ) ) G A =
                ( prod_ k e. ( M ... N ) G A G [_ ( N + 1 ) / k ]_ A ) ) $=
      ( wcel cuz cfv c1 caddc co cfz cprd2 wceq cvv wi prodeq3 imbi2d csb eqidd
      wral elisset cif id opreq123d eqeq12d addex elimel fprodp1s ex dedth 3imp
      syl ) ECHZGFIJHZABHDFGKLMZNMZUCZUSADEOZFGNMZADEOZDURAUAZEMZPZUPEQHZUQUTVF
      RZRZECUDVGVIUQUTUSADVGELUEZOZVBADVJOZVDVJMZPZRZRELEVJPZVHVOUQVPVFVNUTVPVA
      VKVEVMUSADEVJSVPVCVLVDVDEVJVPUFVBADEVJSVPVDUBUGUHTTUQUTVNABQDVJFGELQUIUJU
      KULUMUOUN $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d B y $.  $d M y $.  $d N y $.  $d k y $.
    $( Relation between ` sum_ ` and ` prod_ ` .  (Contributed by FL,
       5-Sep-2010.) $)
    relsumprd $p |- ( N e. ( ZZ>= ` M ) ->
        sum_ k e. ( M ... N ) B = prod_ k e. ( M ... N ) + B ) $=
      ( vy cuz cfv wcel cfz co csu caddc cop cv wceq copab cz cres cseqz cprd2
      dffsum dffprod eqtr4d ) DCFGHCDIJZABKDCLMENAOBEPQRSJGUDABLTEABCDUAEABLCDU
      BUC $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d B k $.  $d M k $.
    fprod1i2.1 $e |- ( k = M -> A = B ) $.
    $( The finite composite of ` A ( k ) ` from ` k = M ` to ` M ` (i.e. a
       composite with only one term) is ` B ` i.e. ` A ( M ) ` .  (Contributed
       by FL, 5-Sep-2010.) $)
    fprod1i2 $p |- ( ( B e. C /\ M e. ZZ /\ G e. D ) ->
                   prod_ k e. ( M ... M ) G A = B ) $=
      ( wcel cz cfz co cprd2 wceq cvv wi elisset caddc cif imbi2d prodeq3 addex
      eqeq1d elimel fprod1i ex dedth syl com3l 3imp ) BCIZGJIZFDIZGGKLZAEFMZBNZ
      UMUKULUPUMFOIZUKULUPPZPZFDQUQUSUKULUNAEUQFRSZMZBNZPZPFRFUTNZURVCUKVDUPVBU
      LVDUOVABUNAEFUTUAUCTTUKULVBABCOEUTGHFROUBUDUEUFUGUHUIUJ $.
      $( [5-Sep-2010] $)
  $}

  ${
    $d B1 k $.  $d B2 a k $.  $d C k $.  $d M k $.
    fprod1b.1 $e |- ( k = M -> A = B1 ) $.
    fprod1b.2 $e |- ( k = ( M + 1 ) -> A = B2 ) $.
    fprod1b.3 $e |- G e. D $.
    $( The finite composite of ` A ( k ) ` from ` k = M ` to ` ( M + 1 ) `
       (i.e. a composite with two terms) is ` ( B1 G B2 ) ` i.e.
       ` ( A ( M ) G A ( M + 1 ) ) ` .  (Contributed by FL, 22-Nov-2010.) $)
    fprod1ib $p |- ( ( B1 e. C /\ B2 e. C /\ M e. ZZ ) ->
                   prod_ k e. ( M ... ( M + 1 ) ) G A = ( B1 G B2 ) ) $=
      ( va wcel c1 caddc co cfz cprd2 wceq a1i cz w3a csb cuz cfv wral 3ad2ant3
      uzid cv wo cpr fzpr eleq2d wi visset elpr orim12i a1d sylbi sylbid eleq1a
      com12 jaao 3adant3 syld r19.21aiv fprodp1s1 syl111anc fprod1i oprex ax-17
      3adant2 csbief opreq12d eqtrd ) GBMZHBMZFUAMZUBZFFNOPZQPZADERZFFQPADERZDV
      TAUCZEPZGHEPVSECMZFFUDUEMZABMZDWAUFWBWESWFVSKTVRVPWGVQFUHUGVSWHDWAVSDUIZW
      AMZAGSZAHSZUJZWHVSWJWIFVTUKZMZWMVSWAWNWIVRVPWAWNSVQFULUGUMWOVSWMWOWIFSZWI
      VTSZUJZVSWMUNWIFVTDUOUPWRWMVSWPWKWQWLIJUQURUSVBUTVPVQWMWHUNVRVPWKWHVQWLGB
      AVAHBAVAVCVDVEVFABCDEFFVGVHVSWCGWDHEVPVRWCGSVQAGBCDEFIKVIVLWDHSVSDLVTAHFN
      OVJLUIHMDVKJVMTVNVO $.
      $( [22-Nov-2010] $)
  $}


