$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Intvl $.

  $( Extend class notation to include intervals. $)
  cintvl $a class Intvl $.

  $( The intervals of ` RR ` . $)
  df-intvl $a |- Intvl =
    ( ( ran (,) u. ( ran (,] u. ( ran [,) u. ran [,] ) ) ) i^i ~P RR ) $.

  $( The set of intervals is a set.  (Contributed by FL, 29-May-2014.) $)
  intvlset $p |- Intvl e. _V $=
    ( cintvl cioo crn cioc cico cicc cun cpw cin cvv df-intvl reex pwex eqeltri
    cr inex2 ) ABCDCECFCGGGZOHZIJKRQOLMPN $.
    $( [29-May-2014] $)


  $( An interval is a part of ` RR ` .  (Contributed by FL, 29-May-2014.) $)
  intrr $p |- ( I e. Intvl -> I C_ RR ) $=
    ( cr wss cioo crn cioc cico cicc cun cpw cin cintvl wcel elpwi adantl sylbi
    wa elin df-intvl eleq2s ) ABCZADEFEGEHEIIIZBJZKZLAUDMAUBMZAUCMZQUAAUBUCRUFU
    AUEABNOPST $.
    $( [29-May-2014] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( A closed-below, open-above interval is connected.  (Contributed by FL,
       30-Mar-2014.) $)
    icccon2 $p |- ( ( A e. RR /\ B e. RR* ) ->
     ( subSp ` <. ( A [,) B ) , ( topGen ` ran (,) ) >. ) e. Con ) $=
      ( vx vy cr wcel cxr wa cv cle wbr w3a clt wi adantr jca imp syl pm2.43i
      ex vz cico cioo crn ctg cfv cop csubsp ccon cicc wss wral 3ad2ant1 adantl
      co rexr elico3 biimpd im2anan9 simplrl simpll1 simpr1 3jca simpll2 simpr2
      letr syl6 com23 simplrr simpr3 simplr3 xrlelttr wb elicore2 sseld anim12d
      elicc2 id anim12i elico1 3imtr4d ssrdv ralrimivv reconn mpbird ) AEFZBGFZ
      HZABUBUOZUCUDUEUFUGUHUFUIFZCIZDIZUJUOZWIUKZDWIULCWIULZWHWNCDWIWIWHWKWIFZW
      LWIFZHZWNWHWRHZUAWMWIWSUAIZEFZWKWTJKZWTWLJKZLZWTGFZAWTJKZWTBMKZLZWTWMFZWT
      WIFZWSXDXHWSXDHXEXFXGXDXEWSXAXBXEXCWTUPUMZUNWSXDXFWHWRXDXFNZWHWRXLNZWHWHX
      MNZWHWHXNWHWHHZWRWHXLXOWRWKEFZAWKJKZWKBMKZLZWLEFZAWLJKZWLBMKZLZHZWHXLNWHW
      PXSWHWQYCWHWPXSABWKUQURWHWQYCABWLUQURUSZYDWHXLYDWHHZXDXFYFXDHZWFXPXALZXQX
      BHZHXFYGYHYIYGWFXPXAYDWFWGXDUTYFXPXDXPXQXRYCWHVAOYFXAXBXCVBVCYGXQXBYFXQXD
      XPXQXRYCWHVDOYFXAXBXCVEPPYHYIXFAWKWTVFQRTTVGVHTSSQQWSXDXGWHWRXDXGNZWHWRYJ
      NZWHWHYKNZWHWHYLXOWRWHYJXOWRYDWHYJNYEYDWHYJYFXDXGYGXEWLGFZWGLZXCYBHZHXGYG
      YNYOYGXEYMWGXDXEYFXKUNYFYMXDYDYMWHYCYMXSXTYAYMYBWLUPUMUNOOYDWFWGXDVIVCYGX
      CYBYFXAXBXCVJYFYBXDXTYAYBXSWHVKOPPYNYOXGWTWLBVLQRTTVGVHTSSQQVCTWSXPXTHZXI
      XDVMWHWRYPWHWPXPWQXTWHWIEWKABVNZVOWHWIEWLYQVOVPQWKWLWTVQRWSAGFZWGHZXJXHVM
      WHYSWRWFYRWGWGAUPWGVRVSOABWTVTRWAWBTWCWHWIEUKWJWOVMYQCDWIWDRWE $.
      $( [30-May-2014] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( An open-below, closed-above interval is connected.  (Contributed by FL,
       30-Mar-2014.) $)
    icccon3 $p |- ( ( A e. RR* /\ B e. RR ) ->
     ( subSp ` <. ( A (,] B ) , ( topGen ` ran (,) ) >. ) e. Con ) $=
      ( vx vy cxr wcel cr wa cv wss cle wbr w3a wi id syl com12 adantr imp jca
      vz cioc co cioo crn ctg cfv cop csubsp ccon cicc wral simpr1 simplll rexr
      anim12i eliocreb sseld 3ad2ant1 adantl 3jca elioc3 biimpd 3ad2ant2 simpr2
      syl6 xrltletr eliocreb2 simpllr simpr3 3ad2ant3 letr ex wb anim12d elicc2
      clt 3imtr4d ssrdv ralrimivv reconn mpbird ) AEFZBGFZHZABUBUCZUDUEUFUGUHUI
      UGUJFZCIZDIZUKUCZWFJZDWFULCWFULZWEWKCDWFWFWEWHWFFZWIWFFZHZWKWEWOHZUAWJWFW
      PUAIZGFZWHWQKLZWQWIKLZMZWRAWQVQLZWQBKLZMZWQWJFZWQWFFZWPXAXDWPXAHZWRXBXCWP
      WRWSWTUMZXGWCWHEFZWQEFZMZAWHVQLZWSHZHXBXGXKXMXGWCXIXJWCWDWOXAUNWPXIXAWEWO
      XIWOWEXIWMWEXINWNWEWMXIWEWFEWHWEWCBEFZHWFEJWCWCWDXNWCOBUOUPABUQPURQRQSRXA
      XJWPWRWSXJWTWQUOUSUTVAXGXLWSWPXLXAWEWOXLWOWEXLWMWEXLNWNWEWMXLWEWMWHGFZXLW
      HBKLZMZXLWEWMXQABWHVBVCXLXOXLXPXLOVDVFQRQSRWPWRWSWTVETTXKXMXBAWHWQVGSPXGW
      RWIGFZWDMZWTWIBKLZHZHXCXGXSYAXGWRXRWDXHWPXRXAWEWOXRWOWEXRWNWEXRNWMWEWNXRW
      EWFGWIABVHZURZQUTQSRWCWDWOXAVIVAXGWTXTWPWRWSWTVJWPXTXAWEWOXTWOWEXTWNWEXTN
      WMWEWNXTWEWNXRAWIVQLZXTMZXTWEWNYEABWIVBVCXTXRXTYDXTOVKVFQUTQSRTTXSYAXCWQW
      IBVLSPVAVMWPXOXRHZXEXAVNWEWOYFWEWMXOWNXRWEWFGWHYBURYCVOSWHWIWQVPPWPWEXFXD
      VNWEWEWOWEORABWQVBPVRVSVMVTWEWFGJWGWLVNYBCDWFWAPWB $.
      $( [30-May-2014] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( An open interval is connected.  (Contributed by FL, 30-Mar-2014.) $)
    icccon4 $p |- ( ( A e. RR* /\ B e. RR* ) ->
     ( subSp ` <. ( A (,) B ) , ( topGen ` ran (,) ) >. ) e. Con ) $=
      ( vx vy cxr wcel wa cv cr wbr w3a clt wi rexr 3ad2ant1 3jca jca imp syl
      wb vz cioo co crn ctg cfv cop csubsp ccon cicc wss wral cle elioo2 biimpd
      anim12d 3ad2ant3 simp2l adantr simp1l2 simp32 xrltletr adantl simp2r 3exp
      simp33 simp1r3 xrlelttr syl6 com23 pm2.43i elioore anim12i elicc2 3imtr4d
      id elioo1 ssrdv ex ralrimivv ioossre a1i reconn mpbird ) AEFZBEFZGZABUBUC
      ZUBUDUEUFUGUHUFUIFZCHZDHZUJUCZWHUKZDWHULCWHULZWGWMCDWHWHWGWJWHFZWKWHFZGZW
      MWGWQGZUAWLWHWRUAHZIFZWJWSUMJZWSWKUMJZKZWSEFZAWSLJZWSBLJZKZWSWLFZWSWHFZWG
      WQXCXGMZWGWQXJMWGWQWGXJWGWQWJIFZAWJLJZWJBLJZKZWKIFZAWKLJZWKBLJZKZGZWGXJMW
      GWOXNWPXRWGWOXNABWJUNUOWGWPXRABWKUNUOUPXSWGXCXGXSWGXCKZXDXEXFXCXSXDWGWTXA
      XDXBWSNOUQZXTWEWJEFZXDKZXLXAGZGXEXTYCYDXTWEYBXDXSWEWFXCURXSWGYBXCXNYBXRXK
      XLYBXMWJNOUSOYAPXTXLXAXKXLXMXRWGXCUTXSWGWTXAXBVAQQYCYDXEAWJWSVBRSXTXDWKEF
      ZWFKZXBXQGZGXFXTYFYGXTXDYEWFYAXSWGYEXCXRYEXNXOXPYEXQWKNOVCOXSWEWFXCVDPXTX
      BXQXSWGWTXAXBVFXOXPXQXNWGXCVGQQYFYGXFWSWKBVHRSPVEVIVJVKRWRXKXOGZXHXCTWQYH
      WGWOXKWPXOWJABVLWKABVLVMVCWJWKWSVNSWRWGXIXGTWGWGWQWGVPUSABWSVQSVOVRVSVTWG
      WHIUKZWIWNTYIWGABWAWBCDWHWCSWD $.
      $( [30-May-2014] $)
  $}

  $( The upper bound belongs to an open-below, closed-above interval.  See
     ~ ubicc2 .  (Contributed by FL, 29-May-2014.) $)
  ubioc1 $p |- ( ( A e. RR* /\ B e. RR* /\ A < B ) -> B e. ( A (,] B ) ) $=
    ( cxr wcel clt wbr w3a cioc co cle simp2 simp3 xrleid 3ad2ant2 3jca 3adant3
    wb elioc1 mpbird ) ACDZBCDZABEFZGZBABHIDZUAUBBBJFZGZUCUAUBUETUAUBKTUAUBLUAT
    UEUBBMNOTUAUDUFQUBABBRPS $.
    $( [29-May-2014] $)


  $( The lower bound belongs to a closed-below, open-above interval.  See
     ~ lbicc2 .  (Contributed by FL, 29-May-2014.) $)
  lbico1 $p |- ( ( A e. RR* /\ B e. RR* /\ A < B ) -> A e. ( A [,) B ) ) $=
    ( cxr wcel clt wbr w3a cico co cle simp1 xrleid 3ad2ant1 simp3 3jca 3adant3
    wb elico1 mpbird ) ACDZBCDZABEFZGZAABHIDZTAAJFZUBGZUCTUEUBTUAUBKTUAUEUBALMT
    UAUBNOTUAUDUFQUBABARPS $.
    $( [29-May-2014] $)

  ${
    $d x A $.  $d x B $.
    $( An empty open interval of extended reals.  (Contributed by FL,
       30-May-2014.) $)
    ico0 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( ( A [,) B ) = (/) <->
                 B <_ A ) ) $=
      ( vx cxr wcel wa c0 wceq cle wbr clt wn wrex wi syl anim12d pm2.43i com12
      cq bitrd cico co cv crab icoval eqeq1d df-ne rabn0 bitr3i xrlelttr 3com23
      wne 3expa rexlimdva w3a qbtwnxr cr qre a1i simpr1 simpl jca xrltle idd ex
      rexr adantr com23 reximdv2 3expia impbid syl5bb xrltnle con4bid ) ADEZBDE
      ZFZABUAUBZGHACUCZIJZVSBKJZFZCDUDZGHZBAIJZVQVRWCGCABUEUFVQWDWEVQWDLZABKJZW
      ELWFWBCDMZVQWGWFWCGULWHWCGUGWBCDUHUIVQWHWGVQWBWGCDVOVPVSDEZWBWGNZVOWIVPWJ
      AVSBUJUKUMUNVOVPWGWHVOVPWGUOZWHWKAVSKJZWAFZCSMZWKWHNCABUPWKWNWHWKWMWBCSDV
      SSEZWMFZWKWIWBFZWPWKWQNWPWKWPWQWOWKWPWQNZNZWMWOWIWSWOVSUQEWIVSURVSVFOZWIW
      KWRWIWKFZWOWIWMWBWOWINXAWTUSXAWLVTWAWAXAVOWIFWLVTNXAVOWIWIVOVPWGUTWIWKVAV
      BAVSVCOXAWAVDPPVEOVGVHQRVIROQVJVKVLABVMTVNT $.
      $( [30-May-2014] $)

    $( An empty open interval of extended reals.  (Contributed by FL,
       30-May-2014.) $)
    ioc0 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( ( A (,] B ) = (/) <->
                 B <_ A ) ) $=
      ( vx cxr wcel wa c0 wceq clt wbr cle wn wrex wi syl anim12d pm2.43i com12
      cq bitrd cioc co cv crab iocval eqeq1d df-ne rabn0 bitr3i xrltletr 3com23
      wne 3expa rexlimdva w3a qbtwnxr cr qre a1i idd simpl simpr2 jca xrltle ex
      rexr adantr com23 reximdv2 3expia impbid syl5bb xrltnle con4bid ) ADEZBDE
      ZFZABUAUBZGHACUCZIJZVSBKJZFZCDUDZGHZBAKJZVQVRWCGCABUEUFVQWDWEVQWDLZABIJZW
      ELWFWBCDMZVQWGWFWCGULWHWCGUGWBCDUHUIVQWHWGVQWBWGCDVOVPVSDEZWBWGNZVOWIVPWJ
      AVSBUJUKUMUNVOVPWGWHVOVPWGUOZWHWKVTVSBIJZFZCSMZWKWHNCABUPWKWNWHWKWMWBCSDV
      SSEZWMFZWKWIWBFZWPWKWQNWPWKWPWQWOWKWPWQNZNZWMWOWIWSWOVSUQEWIVSURVSVFOZWIW
      KWRWIWKFZWOWIWMWBWOWINXAWTUSXAVTVTWLWAXAVTUTXAWIVPFWLWANXAWIVPWIWKVAWIVOV
      PWGVBVCVSBVDOPPVEOVGVHQRVIROQVJVKVLABVMTVNT $.
      $( [30-May-2014] $)

    $( An empty open interval of extended reals.  (Contributed by FL,
       30-May-2014.) $)
    icc0 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( ( A [,] B ) = (/) <->
                 B < A ) ) $=
      ( vx cxr wcel wa cicc co c0 wceq cv cle wbr crab clt iccval eqeq1d wn jca
      bitrd wrex wne df-ne rabn0 bitr3i xrletr 3com23 3expa rexlimdva w3a simp2
      wi simp3 xrleid 3ad2ant2 breq2 breq1 anbi12d rcla4ev 3expia impbid syl5bb
      syl xrlenlt con4bid ) ADEZBDEZFZABGHZIJACKZLMZVJBLMZFZCDNZIJZBAOMZVHVIVNI
      CABPQVHVOVPVHVORZABLMZVPRVQVMCDUAZVHVRVQVNIUBVSVNIUCVMCDUDUEVHVSVRVHVMVRC
      DVFVGVJDEZVMVRULZVFVTVGWAAVJBUFUGUHUIVFVGVRVSVFVGVRUJZVGVRBBLMZFZFVSWBVGW
      DVFVGVRUKWBVRWCVFVGVRUMVGVFWCVRBUNUOSSVMWDCBDVJBJVKVRVLWCVJBALUPVJBBLUQUR
      USVCUTVAVBABVDTVET $.
      $( [30-May-2014] $)

  $}

  $( The lower bound of a closed interval is a member of it. cf. ~ lbicc2 .
     (Contributed by FL, 29-May-2014.) $)
  lbicc2a $p |- ( ( A e. RR* /\ B e. RR* /\ A <_ B ) -> A e. ( A [,] B ) ) $=
    ( cxr wcel cle wbr w3a cicc simp1 xrleid 3ad2ant1 simp3 3jca elicc1 3adant3
    co wb mpbird ) ACDZBCDZABEFZGZAABHPDZSAAEFZUAGZUBSUDUASTUAISTUDUAAJKSTUALMS
    TUCUEQUAABANOR $.
    $( [29-May-2014] $)


  $( The upper bound of a closed interval is a member of it.  (Contributed by
     FL, 29-May-2014.) $)
  ubicc2a $p |- ( ( A e. RR* /\ B e. RR* /\ A <_ B ) -> B e. ( A [,] B ) ) $=
    ( cxr wcel cle wbr w3a cicc simp2 simp3 xrleid 3ad2ant2 3jca elicc1 3adant3
    co wb mpbird ) ACDZBCDZABEFZGZBABHPDZTUABBEFZGZUBTUAUDSTUAISTUAJTSUDUABKLMS
    TUCUEQUAABBNOR $.
    $( [29-May-2014] $)


  ${
    $d x y I $.
    $( All the intervals of ` RR ` are connected.  (Contributed by FL,
       29-May-2014.) $)
    intvconlem1 $p |- ( I e. Intvl ->
    ( subSp ` <. I , ( topGen ` ran (,) ) >. ) e. Con ) $=
      ( vx vy cfv cop csubsp ccon wcel cr wa wi wceq wrex opeq1 fveq2d sylbi ex
      cxr com3l c0 cioo crn ctg cioc cico cicc cun cpw cin cintvl elin wo cv co
      elun bsi icccon4 eleq1 eleq1d imbi12d syl5ibrcom rexlimivv bsi4 ovex elpw
      a1d wss clt wbr w3a ubioc1 ssel2 icccon3 adantr syl5com 3expia pm2.43a wn
      syl cle wb xrlenlt ancoms ioc0 retop csn subsp0 singempcon syl6eqel ax-mp
      ctop a1i syl5ibr syl6bir sylbird pm2.61i bsi3 lbico1 icccon2 adantld ico0
      com12 bsi2 icc0 ubicc2a lbicc2a iccconn com3r a1dd sylc jaoi imp df-intvl
      eleq2s ) AUAUBZUCDZEZFDZGHZAXOUDUBZUEUBZUFUBZUGZUGZUGZIUHZUIZUJAYGHAYEHZA
      YFHZJXSAYEYFUKYHYIXSYHAXOHZAYDHZULYIXSKZAXOYDUOYJYLYKYJABUMZCUMZUAUNZLZCR
      MBRMYLBCAUPYPYLBCRRYMRHZYNRHZJZYLYPYOYFHZYOXPEZFDZGHZKYSUUCYTYMYNUQVFYPYI
      YTXSUUCAYOYFURYPXRUUBGYPXQUUAFAYOXPNOUSUTVAVBPYKAXTHZAYCHZULYLAXTYCUOUUDY
      LUUEUUDAYMYNUDUNZLZCRMBRMYLBCAVCUUGYLBCRRYSYLUUGUUFYFHZUUFXPEZFDZGHZKUUHY
      SUUKUUHUUFIVGZYSUUKKZUUFIYMYNUDVDVEYMYNVHVIZUULUUMKYSUUNUULUUKUUNYSUULUUK
      KZYQYRUUNYSUUOKZYQYRUUNVJZYNUUFHZUUPYMYNVKUULUURYSUUKUULUURUUMUULUURJYNIH
      ZYSUUKUUFIYNVLYQUUSUUKKYRYQUUSUUKYMYNVMQVNVOQSVSVPVQSYSUUNVRZUULUUKYSUUTY
      NYMVTVIZUUOYRYQUVAUUTWAYNYMWBWCZYSUVAUUFTLZUUOYMYNWDUULUUKUVCTXPEZFDZGHZU
      VFUULXPWKHZUVFWEUVGUVETWFGXPWGWHWIWJZWLUVCUUJUVEGUVCUUIUVDFUUFTXPNOUSWMWN
      WOSWPPXBUUGYIUUHXSUUKAUUFYFURUUGXRUUJGUUGXQUUIFAUUFXPNOUSUTVAVBPUUEAYAHZA
      YBHZULYLAYAYBUOUVIYLUVJUVIAYMYNUEUNZLZCRMBRMYLBCAWQUVLYLBCRRYSYLUVLUVKYFH
      ZUVKXPEZFDZGHZKUVMYSUVPUVMUVKIVGZYSUVPKZUVKIYMYNUEVDVEUUNUVQUVRKYSUUNUVQU
      VPUUNYSUVQUVPKZYQYRUUNYSUVSKZUUQYMUVKHZUVTYMYNWRUVQUWAYSUVPUVQUWAUVRUVQUW
      AJYMIHZUVRUVKIYMVLUWBYRUVPYQUWBYRUVPYMYNWSQWTVSQSVSVPVQSYSUUTUVQUVPYSUUTU
      VAUVSUVBYSUVAUVKTLZUVSYMYNXAUWCUVPUVQUWCUVOUVEGUWCUVNUVDFUVKTXPNOUVHWIVFW
      NWOSWPPXBUVLYIUVMXSUVPAUVKYFURUVLXRUVOGUVLXQUVNFAUVKXPNOUSUTVAVBPUVJAYMYN
      UFUNZLZCRMBRMYLBCAXCUWEYLBCRRYSYLUWEUWDYFHZUWDXPEZFDZGHZKUWFYSUWIUWFUWDIV
      GZYSUWIKZUWDIYMYNUFVDVEYNYMVHVIZUWJUWKKYSUWLUWJUWIYSUWLUWDTLZUWJUWIKZYMYN
      XDUWMUWIUWJUWMUWHUVEGUWMUWGUVDFUWDTXPNOUVHWIVFWNSYSUWLVRZUWJUWIYSUWOYMYNV
      TVIZUWNYMYNWBUWPYSUWNYQYRUWPYSUWNKZYQYRUWPVJYNUWDHZYMUWDHZUWQYMYNXEYMYNXF
      UWRUWSUWNYSUWJUWRUWSUWIUWRUWJUWSUWIKZUWJUWRUWJUWTKZUWJUWRJUUSUXAUWDIYNVLU
      WJUWSUUSUWIUWJUWSUUSUWIKZUWJUWSJUWBUXBUWDIYMVLUWBUUSUWIYMYNXGQVSQXHVSQVQS
      XIXJVPVQWOSWPPXBUWEYIUWFXSUWIAUWDYFURUWEXRUWHGUWEXQUWGFAUWDXPNOUSUTVAVBPX
      KPXKPXKPXLPXMXN $.
      $( [29-May-2014] $)

  $}

  $( Declare the derivative of a function. $)
  $c der $.

  $( Extend class notation to include the derivative of a function. $)
  cder $a class der $.

  ${
    $d n i f p x $.
    $( Derivative of a function ` f ` at ` p ` .  Meaningful when the domain of
       ` f ` is an interval of ` RR ` , ` p ` belongs to the domain of ` f ` ,
       the domain of ` f ` is not ` { p } ` and the values of ` f ` are in
       ` ( RR ^m ( 1 ... n ) ) ` .

       Bourbaki doesn't explain why he requires the domain of ` f ` be an
       interval.  Here are some hints.  The domain of ` f ` is an interval,
       ` p ` belongs to the domain of ` f ` and ` dom f = { p } ` guarantee
       ` p ` is not an isolated point in ` dom f ` ( ~ df-islpt ).  We have
       ` ( v i^i ( dom f \ { p } ) ) = ( ( v i^i dom f ) \ { p } ) `
       ( ~ indif2 ) but ` ( v i^i dom f ) =/= { p } ` since ` p ` is not an
       isolated point in ` dom f ` and ` ( v i^i ( dom f \ { p } ) ) =/= (/) `
       what is the condition required by ~ trfil .  And in this case the class
       ` { u | E. v e. ( ( nei `` ( topGen `` ran (,) ) ) `` { p } ) u `
       ` = ( v i^i ( dom f \ { p } ) ) } ` is a filter.  This latter condition
       is required by ~ df-flimfrs and this definition is used by ~ df-der .

       This sort of derivative might be extended easily to work with functions
       ` f ` whose domain is a field ` A ` and whose values are in a
       topological vector space whose scalars are in ` A ` .  The topologies
       would be changed accordingly.  The domain of ` f ` would be a
       neighborhood of ` p ` .  Experimental. $)
    df-der $a |- der = ( n e. NN , i e. Intvl |-> (
      f e. ( ( RR ^m ( 1 ... n ) ) ^m i ) , p e. i |->
      ( ( ( ( topX ` ( x e. ( 1 ... n ) |-> ( topGen ` ran (,) ) ) )
      fLimfrs ( topGen ` ran (,) ) ) ` ( i \ { p } ) ) ` <. p ,
        ( x e. ( i \ { p } ) |-> ( ( ( f ` x ) ( -cv ` n ) ( f ` p ) )
            ( /cv ` n ) ( x - p ) ) ) >. ) ) ) $.
  $}

  ${
    consuba.1 $e |- X = U. J $.
    ${
      $d u v x y A $.  $d u v x y J $.  $d u v x y X $.
      $( Connectedness for a subspace.  See ~ connsub .  (Contributed by FL,
         29-May-2014.) $)
      consuba $p |- ( ( J e. Top /\ A C_ X ) ->
      ( ( subSp ` <. A , J >. ) e. Con <->
         A. x e. J A. y e. J ( ( ( x i^i A ) =/= (/) /\
            ( y i^i A ) =/= (/) /\ ( ( x i^i y ) i^i A ) = (/) )
            -> A =/= ( ( x u. y ) i^i A ) ) ) ) $=
        ( vu vv wcel cvv cin c0 wne wceq w3a cun wi wa eqeq1d imbi12d ctop ccon
        wss cop csubsp cfv cv wral wb simpr cuni uniexg syl5eqel adantr syl2anc
        ssexg stoig3 3adant2 dfcon2 syl elsubspr 3expia im2anan9 neeq1 3anbi13d
        eqid ineq1 uneq1 neeq2d ineq2 3anbi23d uneq2 rcla42v sseq2 anbi2d ax-mp
        biimpd inindir 3anbi3d stoig2 indir neeq12d biimprd 3adant3 syl6 anidms
        a1i com12 com24 pm2.43i imp ralrimivv wrex elsubsp r19.29r neeq2 imbi2d
        eqeq1 imbi1d syl6bi neeq1d syl5ib impcom 3ad2ant2 mpbird 3expd rexlimiv
        syl5ibr a1dd ex com4l impancom impbida bitrd mpd3an3 ) DUAIZCEUCZCJIZCD
        UDUEUFZUBIZAUGZCKZLMZBUGZCKZLMZYAYDKCKZLNZOZCYAYDPCKZMZQZBDUHZADUHZUIXP
        XQRZXQEJIZXRXPXQUJXPYPXQXPEDUKZJFDUAULUMUNCEJUPUOXPXQXROZXTGUGZLMZHUGZL
        MZYSUUAKZLNZOZXSUKZYSUUAPZMZQZHXSUHGXSUHZYNYRXSUAIZXTUUJUIXPXRUUKXQCDJU
        QURGHXSUUFUUFVFUSUTYRUUJYNYRUUJRYLABDDYRUUJYADIZYDDIZRZYLQZYRUUJUUOQYRU
        UNUUJYRYLXPXRUUNUUJYRYLQZQZQZXQXPXRRZUURUUSUUSRZUUNYBXSIZYEXSIZRZUUQUUS
        UULUVAUUSUUMUVBXPXRUULUVAYACDJVAVBXPXRUUMUVBYDCDJVAVBVCUVCUUJYCYFYBYEKZ
        LNZOZUUFYBYEPZMZQZUUPUUIUVIYCUUBYBUUAKZLNZOZUUFYBUUAPZMZQGHYBYEXSXSYSYB
        NZUUEUVLUUHUVNUVOYTYCUUDUVKUUBYSYBLVDZUVOUUCUVJLYSYBUUAVGSVEUVOUUGUVMUU
        FYSYBUUAVHVITUUAYENZUVLUVFUVNUVHUVQUUBYFUVKUVEYCUUAYELVDZUVQUVJUVDLUUAY
        EYBVJSVKUVQUVMUVGUUFUUAYEYBVLVITVMYRUVIYLXPXQUVIYLQZXRYOXPCYQUCZRZUVSEY
        QNZYOUWAQFUWBYOUWAUWBXQUVTXPEYQCVNVOZVQVPUWAYLUVIUWAYIUVFYKUVHUWAYHUVEY
        CYFUWAYGUVDLYGUVDNZUWAYAYDCVRZWGSVSUWACUUFYJUVGCDVTZYJUVGNZUWAYAYDCWAZW
        GWBTWCUTWDWHWEWEWFURWIWJWKWLYRYNRUUIGHXSXSYRYNYSXSIZUUAXSIZRZUUIQZYRYNU
        WLQYRUWKYNYRUUIXPXRUWKYNYRUUIQZQZQZXQUUSUWOUUTUWKUVOADWMZUVQBDWMZRUWNUU
        SUWIUWPUUSUWJUWQUUSUWIUWPAYSCDJWNVQUUSUWJUWQBUUACDJWNVQVCUWPYNUWQUWMUWP
        YNRUVOYMRZADWMUWQUWMQZUVOYMADWOUWRUWSADUWRUULUWSYMUVOUULUWSQUWQYMUVOUUL
        UWMUWQYMUVOUULUWMQQZUWQYMRUVQYLRZBDWMUWTUVQYLBDWOUXAUWTBDUXAUWTQUUMUXAU
        VOUWMUULUVQYLUVOUWMQUVQYLUVOYRUUIYLUVOYROZUUIUVQYTYFYSYEKZLNZOZUUFYSYEP
        ZMZQZUXBUXHUVIYLYRUVIUVOYRYLUVIXPXQYLUVIQZXRUWBYOUXIQFUWBYOUWAUXIUWCYLU
        VFCUVGMZQZUWAUVIUWGYLUXKQUWHUWGYLYIUXJQZUXKUWGYKUXJYIYJUVGCWPWQUWDUXLUX
        KQUWEUWDUXLUXKUWDYIUVFUXJUWDYHUVEYCYFYGUVDLWRVSWSVQVPWTVPUWAUXJUVHUVFUW
        ACUUFUVGUWFXAWQXBWTVPWDXCURUVOYLUXHUVIUIYRUVOUXEUVFUXGUVHUVOYTYCUXDUVEY
        FUVPUVOUXCUVDLYSYBYEVGSVEUVOUXFUVGUUFYSYBYEVHVITXDXEUVQUUEUXEUUHUXGUVQU
        UBYFUUDUXDYTUVRUVQUUCUXCLUUAYEYSVJSVKUVQUUGUXFUUFUUAYEYSVLVITXHXFWKXIWG
        XGUTXJXKXCWHXGUTXLWEWFURWIWJWKWLXMXNXO $.
        $( [29-May-2014] $)
    $}

    ${
      $d x y A $.  $d x y J $.  $d x y U $.  $d x y V $.  $d x y X $.
      $( Non-connectedness for a subspace.  (Contributed by FL,
         29-May-2014.) $)
      nconsuba $p |- ( ( ( J e. Top /\ A C_ X ) /\ ( ( U e. J /\ V e. J ) /\
        ( ( U i^i A ) =/= (/) /\ ( V i^i A ) =/= (/) /\ ( U i^i V ) = (/) )
           /\ A C_ ( U u. V ) ) )
             -> -. ( subSp ` <. A , J >. ) e. Con ) $=
        ( vx vy wcel wa cin c0 wne wceq w3a cun wi wn ineq1 ineq1d ctop wss cop
        csubsp cfv ccon cv wral simpr1l simpr1r simpr21 simpr22 incom in0 eqtri
        wrex syl6eq 3ad2ant3 3ad2ant2 adantl 3jca simpr3 df-ss sylib nne sylibr
        syl5req pm4.61 sylanbrc neeq1d eqeq1d uneq1 neeq2d imbi12d notbid ineq2
        3anbi13d 3anbi23d rcla42ev syl3anc wb rexnal bicomi a1i rexbidva mpbird
        uneq2 consuba adantr mtbird ) CUAIAEUBJZBCIZDCIZJZBAKZLMZDAKZLMZBDKZLNZ
        OZABDPZUBZOZJZACUCUDUEUFIZGUGZAKZLMZHUGZAKZLMZXGXJKZAKZLNZOZAXGXJPZAKZM
        ZQZHCUHZGCUHZXEYARZGCUPZYBRXEYDXTRZHCUPZGCUPZXEWLWMWPWRWSAKZLNZOZAXBAKZ
        MZQZRZYGWLWMXAXCWKUIWLWMXAXCWKUJXEYJYLRZYNXEWPWRYIWPWRWTWNXCWKUKWPWRWTW
        NXCWKULXDYIWKXAWNYIXCWTWPYIWRWTYHLAKZLWSLASYPALKLLAUMAUNUOUQURUSUTVAXEA
        YKNYOXEYKAXBKZAXBAUMXEXCYQANWKWNXAXCVBAXBVCVDVGAYKVEVFYJYLVHVIYEYNWPXLB
        XJKZAKZLNZOZABXJPZAKZMZQZRGHBDCCXGBNZXTUUEUUFXPUUAXSUUDUUFXIWPXOYTXLUUF
        XHWOLXGBASVJUUFXNYSLUUFXMYRAXGBXJSTVKVQUUFXRUUCAUUFXQUUBAXGBXJVLTVMVNVO
        XJDNZUUEYMUUGUUAYJUUDYLUUGXLWRYTYIWPUUGXKWQLXJDASVJUUGYSYHLUUGYRWSAXJDB
        VPTVKVRUUGUUCYKAUUGUUBXBAXJDBWGTVMVNVOVSVTXEYCYFGCYCYFWAXEXGCIJYFYCXTHC
        WBWCWDWEWFYAGCWBVDWKXFYBWAXDGHACEFWHWIWJ $.
        $( [29-May-2014] $)
    $}

    ${
      $d x y A $.  $d x y J $.  $d x y U $.  $d x y V $.  $d x y X $.
      $( Non-connectedness for a subspace.  (Contributed by FL,
         29-May-2014.) $)
      nconsubb $p |- ( ( ( J e. Top /\ A C_ X ) /\ ( ( U e. J /\ V e. J ) /\
        ( ( U i^i A ) =/= (/) /\ ( V i^i A ) =/= (/) /\
          ( ( U i^i V ) i^i A ) = (/) ) /\ A C_ ( U u. V ) ) )
             -> -. ( subSp ` <. A , J >. ) e. Con ) $=
        ( vx vy wcel wa cin c0 wne wceq w3a cun wi wn wrex ineq1d ctop wss ccon
        cop csubsp cfv cv wral simpr1l simpr1r simpr2 df-ss incom eqeq2i biimpi
        eqcoms sylbi 3ad2ant3 adantl sylibr pm4.61 sylanbrc ineq1 neeq1d eqeq1d
        nne 3anbi13d uneq1 neeq2d imbi12d ineq2 3anbi23d uneq2 rcla42ev syl3anc
        notbid wb rexnal bicomi a1i rexbidva mpbird sylib consuba adantr mtbird
        ) CUAIAEUBJZBCIZDCIZJZBAKZLMZDAKZLMZBDKZAKZLNZOZABDPZUBZOZJZACUDUEUFUCI
        ZGUGZAKZLMZHUGZAKZLMZXDXGKZAKZLNZOZAXDXGPZAKZMZQZHCUHZGCUHZXBXRRZGCSZXS
        RXBYAXQRZHCSZGCSZXBWHWIWRAWSAKZMZQZRZYDWHWIWRWTWGUIWHWIWRWTWGUJXBWRYFRZ
        YHWGWJWRWTUKXBAYENZYIXAYJWGWTWJYJWRWTAWSKZANYJAWSULYJAYKAYKNYJYKYEAAWSU
        MUNUOUPUQURUSAYEVFUTWRYFVAVBYBYHWLXIBXGKZAKZLNZOZABXGPZAKZMZQZRGHBDCCXD
        BNZXQYSYTXMYOXPYRYTXFWLXLYNXIYTXEWKLXDBAVCVDYTXKYMLYTXJYLAXDBXGVCTVEVGY
        TXOYQAYTXNYPAXDBXGVHTVIVJVPXGDNZYSYGUUAYOWRYRYFUUAXIWNYNWQWLUUAXHWMLXGD
        AVCVDUUAYMWPLUUAYLWOAXGDBVKTVEVLUUAYQYEAUUAYPWSAXGDBVMTVIVJVPVNVOXBXTYC
        GCXTYCVQXBXDCIJYCXTXQHCVRVSVTWAWBXRGCVRWCWGXCXSVQXAGHACEFWDWEWF $.
        $( [29-May-2014] $)
    $}

  $}

  $( Hard to describe.  A picture can help.  (Contributed by FL,
     29-May-2014.) $)
  hdrmp $p |- ( ( ( A i^i B ) =/= (/) /\ ( C =/= (/) /\ D =/= (/) ) /\
   ( A u. B ) = ( C u. D ) )
      -> ( ( ( A i^i C ) =/= (/) /\ ( A i^i D ) =/= (/) ) \/
         ( ( B i^i C ) =/= (/) /\ ( B i^i D ) =/= (/) ) ) ) $=
    ( cin c0 wceq wne wa cun wi eqtr expcom adantr syl ex eqcoms com3l 3imp wss
    w3a uneq12 un0 indi uncom ineq2i ineq2 inabs neiopne nonconne pm2.21i com12
    wo a1i syl5com mpsyl com24 sylancr sylancl ineq1 incom indir uneq1 pm13.181
    wn mp2an imp 3adant1 impcom eqss unss disjssun inss1 sstr inidm ssrin sseq1
    wb bi1 sseq2 ss0 syl6bi ax-mp necon3d a1dd mpan syl6 3impd 3exp sylbi inass
    sylbir in0 syl6eq syl5eq necon3i jca olcd pm2.61i com23 syl6com syl5 inabs2
    adantl com4l df-ne orc pm2.61ine ) ACEZFGZABEZFHZCFHZDFHZIZABJZCDJZGZUAZXIF
    HZADEZFHZIZBCEZFHZBDEZFHZIZUMZKZYAFGZXJYJKYKXJYJYKXJIYAXIJZFFJZGZYMFGZYJYAF
    XIFUBFUCYNYOIADCJZEZYLGZYLFGZYJADCUDYLYMFLYRYSIAXQEZYQGZYQFGZYJXQYPACDUEZUF
    YQYLFLUUAUUBIYTFGZXSYIYTYQFLXLXOXRUUDYIKZXRXLXOUUEXRAXPEZYTGZXLXOUUEKKXPXQA
    UGUUGUUDXOXLYIUUGUUDXOXLYIKZKZUUFAGUUGUUDIUUFFGZUUIABUHUUFYTFLUUJUUIKAUUFAU
    UFGZUUJUUIUUKUUJIAFGZXOUUHAUUFFLUULUUHKXOXLUULYIXLAFHZBFHZIUULYIKZABUIUUMUU
    OUUNUULUUMYIUULUUMIYIAFUJUKMNOULUNUOPQUPPUQORSUOURURUSPYKVEZXJXSYIUUPXJXSUA
    ZYHYCUUQYEYGXJXSYEUUPXSXJYEXOXRXJYEKZXLXOXRUURXMXRUURKXNXRXMUURXRXPCEZXQCEZ
    GZXMUURKZXPXQCUTUUTCXQEZGZUVCCGZUVAUVBKZXQCVACDUHUVDUVEIUUTCGZUVFUUTUVCCLUV
    AUVGUVBUUSXIYDJZGUVAUVGIUUSCGZUVBABCVBUUSUUTCLUVIUVBKUVHUUSUVHUUSGZUVIUVBUV
    JUVIIUVHCGZUVBUVHUUSCLXJUVKXMYEXJUVHFYDJZGZUVKXMYEKZKZXIFYDVCUVLYDFJZGZUVPY
    DGZUVMUVOKZFYDUEYDUCUVQUVRIUVLYDGZUVSUVLUVPYDLUVMUVTUVOUVMUVTIUVHYDGUVOUVHU
    VLYDLUVOYDUVHYDUVHGZUVKUVNUWAUVKIYDCGZUVNYDUVHCLUWBXMYEYDCFVDPOPQOMOVFOROPQ
    UPMOVFOULNVGVHVIVHUUQXKDEZFHZYGUUPXJXSUWDXSUUPXJUWDXLXOXRUUPXJUWDKKZXRXLXOU
    WEXRXPXQTZXQXPTZIZXLXOUWEKKZXPXQVJZUWFUWIUWGUWFAXQTZBXQTZIZUWIABXQVKZUWKUWI
    UWLUWKXLXOUWEXJUWKXLXOUAZUUPUWDXJUWKADTZVRZUWOUUPUWDKZKACDVLUWQUWKXLXOUWRUW
    QUWKUWPXLXOUWRKKZUWKUWPVSXKATZUWPUWSABVMZUWTUWPIXKDTZUWSXKADVNUXBXLUWRXOUXB
    XLUWDUUPUXBUWCFXKFXKXKEZXKGZUXBUXCDXKEZTZUWCFGZXKFGZKZXKVOZXKDXKVPUXDUXFXKU
    XETZUXIUXCXKUXEVQUWCUXEGUXKUXIKZXKDVAUXLUXEUWCUXEUWCGUXKXKUWCTZUXIUXEUWCXKV
    TUXGUXMUXHUXGUXMXKFTZUXHUWCFXKVTXKWAZWBULWBQWCWBUPWDWEWEOWFWGWHORWINWLNWJRS
    RSYFFUWCFYFFGZUWCAYFEZFABDWKUXPUXQAFEZFYFFAUGAWMZWNWOWPOWQWRWIWSXJVEZYJKYAF
    YKUXTXSYIYKUXTXSUAZYHYCUYAYEYGUYAXKCEZFHZYEYKUXTXSUYCXSYKUXTUYCXLXOXRYKUXTU
    YCKZKZXRXLXOUYEXRUWHXLXOUYEKKZUWJUWFUYFUWGUWFUWMUYFUWNUWKUYFUWLUWKXLXOUYEYK
    AYPTZACTZVRZUWOUYDADCVLUWKXLXOUYIUYDKZXQYPGZUWKXLXOUYJKKZKUUCUYKUWKUYGUYLXQ
    YPAVTUYGUYIXOXLUYDUYIUYGUYHXOXLUYDKZKZUYGUYHVSUWTUYHUYNUXAUWTUYHIXKCTZXOUYM
    XKACVNUYOUYMKXOUYOXLUYCUXTUYOUYBFXKFUXDUYOUXCCXKEZTZUYBFGZUXHKZUXJXKCXKVPUX
    DUYQXKUYPTZUYSUXCXKUYPVQUYPUYBGZUYTUYSKCXKVAVUAUYRUYTUXHVUAUYRUYTUXHKZVUAUY
    RIUYPFGZVUBUYPUYBFLVUCUYTUXNUXHUYPFXKVTUXOWBOPWTWCWBUPWDWEUNUOWFXAUQWBWCSXB
    WINWLNWJRSRSYDFUYBFYDFGZUYBAYDEZFABCWKVUDVUEUXRFYDFAUGUXSWNWOWPOYKUXTXSYGXS
    YKUXTYGXLXOXRYKUXTYGKZKZXRXLXOVUGXPDEZYAYFJZGXRVUHXQDEZGZXLXOVUGKKZABDVBXPX
    QDUTVUKVULKVUIVUHVUIVUHGZVUKVULVUMVUKIVUIVUJGZVULVUIVUHVUJLYKVUNXLXOVUFYKVU
    IFYFJZGZVUNXLXOVUFKZKZKZYAFYFVCVUOYFFJZGZVUTYFGZVUPVUSKZFYFUEYFUCVVAVVBIVUO
    YFGZVVCVUOVUTYFLVUPVVDVUSVUPVVDIVUIYFGVUSVUIVUOYFLVUSYFVUIYFVUIGZVUNVURVVEV
    UNIYFVUJGZVURYFVUIVUJLVUJDXQEZGZVVGDGZVVFVURKZXQDVACDXCVVHVVIIVUJDGZVVJVUJV
    VGDLVVFVVKVURVVFVVKIYFDGZXLVUQYFVUJDLVVLVUQKXLXOVVLVUFXNVVLVUFKXMXNVVLYGUXT
    VVLXNYGYFDFVDMWEXDULUNUOMOVFOPQOMOVFOXEOPQUPRSRSWQWRWIUXTYBYJUXTXTYBYJKXIFX
    FXTYBYIXSXTYBYIYCYHXGPWEWLULXHWS $.
    $( [29-May-2014] $)

  ${
    $d k u v x A $.  $d u v x B $.  $d k u v J $.  $d k x P $.  $d k u v x X $.
    $d u v x ph $.  $d k x U $.  $d k x V $.
    iuncon.1 $e |- X = U. J $.
    iuncon.2 $e |- ( ph -> J e. Top ) $.
    iuncon.3 $e |- ( ( ph /\ k e. A ) -> B C_ X ) $.
    iuncon.4 $e |- ( ( ph /\ k e. A ) -> P e. B ) $.
    iuncon.5 $e |- ( ( ph /\ k e. A ) -> ( subSp ` <. B , J >. ) e. Con ) $.
    ${
      iuncon.6 $e |- ( ph -> U e. J ) $.
      iuncon.7 $e |- ( ph -> V e. J ) $.
      iuncon.8 $e |- ( ph -> ( U i^i U_ k e. A B ) =/= (/) ) $.
      iuncon.9 $e |- ( ph -> ( V i^i U_ k e. A B ) =/= (/) ) $.
      iuncon.10 $e |- ( ph -> ( U i^i V ) C_ ( X \ U_ k e. A B ) ) $.
      iuncon.11 $e |- ( ph -> U_ k e. A B C_ ( U u. V ) ) $.
      iuncon.12 $e |- ( ph -> A. k ph ) $.
      $( Lemma for ~ iuncom . $)
      iunconlem $p |- ( ph -> -. P e. U ) $=
        ( vx cv ciun cin wcel wex wn c0 wne n0 sylib elin wrex eliun ax-17 hban
        wa cop csubsp cfv ccon adantr ctop wss wceq w3a cun ad2antrr jca simprr
        inelcm syl2anc ad2antrl cdif ssiun2 ad2antlr sscon syl sstrd inss1 cuni
        wb elssuni syl6sseqr syl5ss reldisj mpbird 3jca nconsubb expr mt2d an4s
        syl23anc exp32 rexlimd syl5bi expimpd exlimdv mpd ) AUBUCZHFBCUDZUEZUFZ
        UBUGZDEUFZUHZAXCUIUJXERUBXCUKULAXDXGUBXDXAHUFZXAXBUFZURAXGXAHXBUMAXHXIX
        GXIXACUFZFBUNAXHURZXGFXABCUOXKXJXGFBAXHFUAXHFUPUQXGFUPXKFUCBUFZXJXGAXLX
        HXJXGAXLURZXHXJURZURXFCGUSUTVAVBUFZXMXOXNNVCXMXNXFXOUHZXMXNXFURZURZGVDU
        FZCIVEZEGUFZHGUFZURECUEUIUJZHCUEUIUJZEHUEZCUEUIVFZVGCEHVHZVEXPAXSXLXQKV
        IXMXTXQLVCXRYAYBAYAXLXQOVIZAYBXLXQPVIVJXRYCYDYFXRXFDCUFZYCXMXNXFVKXMYIX
        QMVCDECVLVMXNYDXMXFXAHCVLVNXRYFYEICVOZVEZXRYEIXBVOZYJAYEYLVEXLXQSVIXRCX
        BVEZYLYJVEXLYMAXQFBCVPVQZCXBIVRVSVTXRYEIVEYFYKWCXRYEEIEHWAXRYAEIVEYHYAE
        GWBIEGWDJWEVSWFYECIWGVSWHWIXRCXBYGYNAXBYGVEXLXQTVIVTCEGHIJWJWNWKWLWMWOW
        PWQWRWQWSWT $.
        $( [11-Jun-2014] $)
    $}

    $d k ph $.
    $( The indexed union of connected overlapping subspaces sharing a common
       point is connected.  (Contributed by Mario Carneiro, 11-Jun-2014.) $)
    iuncon $p |- ( ph -> ( subSp ` <. U_ k e. A B , J >. ) e. Con ) $=
      ( vu vv vx wcel cv c0 wa ax-17 ciun cop cfv ccon cin wne cdif wss w3a cun
      csubsp wn wi wral simprr simprl1 wex n0 inss2 sseli eliun rexn0 sylbi syl
      wrex exlimiv ralrimiva ad2antrr r19.2z syl2anc sylibr sseldd simpll sylan
      ctop simplrl simplrr simprl2 simprl3 hbiu1 hbin hbne hbdif hbss hbun hban
      wo hb3an iunconlem incom syl5eqss uncom syl6sseq jca pm4.56 sylib sylnibr
      elun pm2.65i pm2.21i expr pm2.01d ex ralrimivva wb iunss connsub mpbird )
      AEBCUAZFUBUKUCUDPZMQZXIUEZRUFZNQZXIUEZRUFZXKXNUEZGXIUGZUHZUIZXIXKXNUJZUHZ
      ULZUMZNFUNMFUNZAYDMNFFAXKFPZXNFPZSZSZXTYCYIXTSYBYIXTYBYCYIXTYBSZSZYCYKDYA
      PZYKXIYADYIXTYBUOZYKDCPZEBVEZDXIPYKBRUFZYNEBUNZYOYKXMYPXMXPXSYBYIUPZXMXNX
      LPZNUQYPNXLURYSYPNYSXNXIPZYPXLXIXNXKXIUSUTYTXNCPZEBVEYPEXNBCVAUUAEBVBVCVD
      VFVCVDAYQYHYJAYNEBKVGVHYNEBVIVJEDBCVAVKVLYKDXKPZDXNPZWGZYLYKUUBULZUUCULZS
      UUDULYKUUEUUFYKBCDXKEFXNGHYKAFVOPZAYHYJVMZIVDZYKAEQBPZCGUHZUUHJVNZYKAUUJY
      NUUHKVNZYKAUUJCFUBUKUCUDPUUHLVNZAYFYGYJVPZAYFYGYJVQZYRXMXPXSYBYIVRZXMXPXS
      YBYIVSZYMYIYJEYIETXTYBEXMXPXSEEOXLREOXKXIOQZXKPETZEOBCVTZWAUUSRPETZWBEOXO
      REOXNXIUUSXNPETZUVAWAUVBWBEOXQXRUUSXQPETEOGXIUUSGPETUVAWCWDWHEOXIYAUVAEOX
      KXNUUTUVCWEWDWFWFZWIYKBCDXNEFXKGHUUIUULUUMUUNUUPUUOUUQYRYKXNXKUEXQXRXNXKW
      JUURWKYKXIYAXNXKUJYMXKXNWLWMUVDWIWNUUBUUCWOWPDXKXNWRWQWSWTXAXBXCXDAUUGXIG
      UHZXJYEXEIAUUKEBUNUVEAUUKEBJVGEBCGXFVKMNXIFGHXGVJXH $.
      $( [11-Jun-2014] $)
  $}

  ${
    $d k x A $.  $d k x B $.  $d k x J $.  $d k x X $.
    uncon.1 $e |- X = U. J $.
    $( The union of two connected overlapping subspaces is connected.
       (Contributed by FL, 29-May-2014.)  (The proof was shortened by Mario
       Carneiro, 11-Jun-2014.) $)
    uncon $p |- ( ( J e. Top /\ ( A C_ X /\ B C_ X ) /\ ( A i^i B ) =/= (/) )
      ->
     ( ( ( subSp ` <. A , J >. ) e. Con /\ ( subSp ` <. B , J >. ) e. Con ) ->
      ( subSp ` <. ( A u. B ) , J >. ) e. Con ) ) $=
      ( vx vk wcel wss wa cop csubsp cfv ccon wceq biimprd jaoa syl5com imp cvv
      ctop cin c0 wne cun wi cv wex n0 w3a cpr ciun simpl1 elpr simpl2l simpl2r
      vex jca sseq1 sylan2b simpl3 sylib eleq2 simpr opeq1 fveq2d eleq1d iuncon
      wo elin cuni uniiun topopn syl ssexg syl2anc uniprg syl5eqr opeq1d 3expia
      mpbid ex exlimdv syl5bi 3impia ) CUAHZADIZBDIZJZABUBZUCUDZACKZLMZNHZBCKZL
      MZNHZJZABUEZCKZLMZNHZUFZWKFUGZWJHZFUHWFWIJZXCFWJUIXFXEXCFWFWIXEXCWFWIXEUJ
      ZWRXBXGWRJZGABUKZGUGZULZCKZLMZNHXBXHXIXJXDGCDEWFWIXEWRUMZXJXIHZXHXJAOZXJB
      OZVIZXJDIZXJABGUQUNZXHXRXSXHWIXRXSXHWGWHWGWHWFXEWRUOZWGWHWFXEWRUPZURXPWGX
      SXQWHXPXSWGXJADUSPXQXSWHXJBDUSPQRSUTXOXHXRXDXJHZXTXHXRYCXHXDAHZXDBHZJZXRY
      CXHXEYFWFWIXEWRVAXDABVJVBXPYDYCXQYEXPYCYDXJAXDVCPXQYCYEXJBXDVCPQRSUTXOXHX
      RXJCKZLMZNHZXTXHXRYIXHWRXRYIXGWRVDXPWNYIXQWQXPYIWNXPYHWMNXPYGWLLXJACVEVFV
      GPXQYIWQXQYHWPNXQYGWOLXJBCVEVFVGPQRSUTVHXHXMXANXHXLWTLXHXKWSCXHXKXIVKZWSG
      XIVLXHATHZBTHZYJWSOXHWGDCHZYKYAXHWFYMXNCDEVMVNZADCVOVPXHWHYMYLYBYNBDCVOVP
      ABTTVQVPVRVSVFVGWAWBVTWCWDWE $.
      $( [11-Jun-2014] $) $( [29-May-2014] $)
  $}

  ${
    ${
      $d f i n p x I $.  $d f p x F $.  $d f i n p x N $.  $d f p x P $.
      isder.1 $e |- J =
        ( topX ` ( x e. ( 1 ... N ) |-> ( topGen ` ran (,) ) ) ) $.
      isder.2 $e |- K = ( topGen ` ran (,) ) $.
      isder.4 $e |- S = ( x e. ( I \ { P } )
        |-> ( ( ( F ` x ) ( -cv ` N ) ( F ` P ) ) ( /cv ` N ) ( x - P ) ) ) $.
      isder.5 $e |- D = ( N der I ) $.
      $( The derivative of ` F ` at point ` P ` is the limit of the slope
         ` F ( x ) - F ( P ) / x - P ` when ` x ` tends to ` P ` .  (Bourbaki
         FVR I.11 def. 1) (Contributed by FL, 29-May-2014.) $)
      isder $p |- ( ( ( N e. NN /\ I e. Intvl /\ P e. I ) /\ ( I =/= { P } /\
        F : I --> ( RR ^m ( 1 ... N ) ) ) ) -> ( F D P ) =
        ( ( ( J fLimfrs K ) ` ( I \ { P } ) ) ` <. P , S >. ) ) $=
        ( vf vp wcel co cmap cfv wceq vn vi cn cintvl w3a csn wne cr c1 wf cder
        cfz wa cdif cmcv cmin cdivcv cmpt cop cioo crn ctg ctopx cflimfrs cmpt2
        cv oveqi a1i cvv simpl1 simpl2 ovex mpt2exga sylancr simpl oveq2d simpr
        wral oveq12d wel mpteq1 syl fveq2d difeq1d fveq12d oveqd eqidd oveq123d
        oveq1d mpteq12dv opeq2d adantr ralrimivva syl3anc df-der ovmpt2x simpr2
        mpt2eq123 wi wb elmapg exbiri com23 imp impcom simpl3 fvex sneq difeq2d
        adantl fveq1d opeq12d eqid eqcomi eqtrd 3eqtrd ) IUCPZFUDPZCFPZUEZFCUFZ
        UGZFUHUIIULQZRQZEUJZUMZUMZECBQZECIFUKQZQZECNOYDFRQZFOVFZAFYLUFZUNZAVFZN
        VFZSZYLYPSZIUOSZQZYOYLUPQZIUQSZQZURZUSZYNAYCUTVAVBSZURZVCSZUUFVDQZSZSZV
        EZQZCDUSZFYAUNZGHVDQZSZSZYHYJTYGBYIECMVGVHYGYIUULECYGXQXRUULVIPZYIUULTX
        QXRXSYFVJXQXRXSYFVKZYGYKVIPXRUUSYDFRVLUUTNOYKFUUKVIUDVMVNUAUBIFUCUDNOUH
        UIUAVFZULQZRQZUBVFZRQZUVDYLAUVDYMUNZYQYRUVAUOSZQZUUAUVAUQSZQZURZUSZUVFA
        UVBUUFURZVCSZUUFVDQZSZSZVEZUULUKVIUDUVAITZUVDFTZUMZUVEYKTUVTUVQUUKTZOUV
        DVRNUVEVRUVRUULTUWAUVCYDUVDFRUWAUVBYCUHRUWAUVAIUIULUVSUVTVOZVPZVPUVSUVT
        VQZVSUWEUWAUWBNOUVEUVDUWAUWBYPUVEPOUBVTUMUWAUVLUUEUVPUUJUWAUVFYNUVOUUIU
        WAUVNUUHUUFVDUWAUVMUUGVCUWAUVBYCTUVMUUGTUWDAUVBYCUUFWAWBWCWIUWAUVDFYMUW
        EWDZWEUWAUVKUUDYLUWAAUVFUVJYNUUCUWFUWAUVHYTUUAUUAUVIUUBUWAUVAIUQUWCWCUW
        AUVGYSYQYRUWAUVAIUOUWCWCWFUWAUUAWGWHWJWKWEWLWMNOUVEUVDUVQYKFUUKWRWNUVSU
        DWGANUBUAOWOWPWNWFYGUUMCAUUOYOESZCESZYSQZYOCUPQZUUBQZURZUSZUUOUUISZSZUU
        RYGEYKPZXSUWOVIPZUUMUWOTYFXTUWPYBYEXTUWPWSYBXTYEUWPYBXTUWPYEYBXTUMYDVIP
        XRUWPYEWTUHYCRVLYBXQXRXSWQYDFEVIUDXAVNXBXCXDXEXQXRXSYFXFUWQYGUWMUWNXGVH
        NOECYKFUUKUWOUULVIFYPETZYLCTZUMZUUEUWMUUJUWNUWTYNUUOUUIUWTYMYAFUWSYMYAT
        UWRYLCXHXJXIZWCUWTYLCUUDUWLUWRUWSVQZUWTAYNUUCUUOUWKUXAUWTYTUWIUUAUWJUUB
        UWTYQUWGYRUWHYSUWTYOYPEUWRUWSVOZXKUWTYLCYPEUXCUXBWEVSUWTYLCYOUPUXBVPVSW
        JXLWEUWRFWGUULXMWPWNYGUWMUUNUWNUUQYGUUOUUIUUPYGUUHGUUFHVDUUHGTYGGUUHJXN
        VHUUFHTYGHUUFKXNVHVSXKYGUWLDCUWLDTYGDUWLLXNVHWKWEXOXP $.
        $( [29-May-2014] $)
    $}

  $}

