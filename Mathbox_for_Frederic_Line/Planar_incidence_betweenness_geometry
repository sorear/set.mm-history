$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Planar incidence betweenness geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Plibg $.

  $( Extend class notation with the class of all planar incidence betweenness
     geometries. $)
  cplibg $a class Plibg $.

  $c Plibg0 $.

  $( Extend class notation with the class of all planar incidence betweenness
     geometries.  Part 0 of the definition. $)
  cplibg0 $a class Plibg0 $.

  $c Plibg1 $.

  $( Extend class notation with the class of all planar incidence betweenness
     geometries.  Part 1 of the definition. $)
  cplibg1 $a class Plibg1 $.

  $c Plibg2 $.

  $( Extend class notation with the class of all planar incidence betweenness
     geometries.  Part 2 of the definition. $)
  cplibg2 $a class Plibg2 $.

  $c Plibg3 $.

  $( Extend class notation with the class of all planar incidence betweenness
     geometries.  Part 3 of the definition. $)
  cplibg3 $a class Plibg3 $.

  $c Plibg4a $.

  $( Extend class notation with the class of all planar incidence betweenness
     geometries.  Part 4a of the definition. $)
  cplibg4a $a class Plibg4a $.

  $c Plibg4b $.

  $( Extend class notation with the class of all planar incidence betweenness
     geometries.  Part 4b of the definition. $)
  cplibg4b $a class Plibg4b $.

  $c seg $.

  $( Extend class notation with the class of all planar incidence betweenness
     geometries. $)
  cseg $a class seg $.

  ${
    $d l b h x y z p $.
    $( Definition of a segment.  Meaningful if ` <. l , b >. ` is an
       incidence-betweenness geometry. $)
    df-seg $a |- seg = { <. <. l , b >. , h >. | h = { <. <. x , y >. , z >. |
      ( x e. U. l /\ y e. U. l /\ z =
      { p e. U. l | ( <. <. x , p >. , y >. e. b \/ p = x \/ p = y ) } ) } } $.
  $}

  ${
    $d B b l p w x y z $.  $d L b l w x y z $.  $d P b l p w x y z $.
    isseg1.1 $e |- P = U. L $.
    $( The values of the function ` ( seg `` <. L , B >. ) ` are the segments
       ` <. x , y >. ` . $)
    isseg1 $p |- ( ( L e. A /\ B e. C ) ->
      ( seg ` <. L , B >. ) = { <. <. x , y >. , z >. |
      ( x e. P /\ y e. P /\ z =
      { p e. P | ( <. <. x , p >. , y >. e. B \/ p = x \/ p = y ) } ) } ) $=
      ( vl vb vw wcel wa cseg co cv cop wceq w3o crab w3a copab2 cfv cvv
      df-3an oprabbii oprabex2g cuni uniexg syl5eqel sylanc wi unieq syl6eqr
      eleq2d rabeq syl eqeq2d 3anbi123d oprabbidv wb eleq2 adantr biidd
      3orbi123d rabbidva 3anbi3d cxp cres df-seg wrel cdm reloprab reldmoprab
      resid2 mp2an resoprab 3eqtr2i oprabval2g 3expia elisset syl2an ex mpid
      imp df-opr syl5eqr ) HDNZEFNZOHEPQZARZGNZBRZGNZCRZWMIRZSWOSZENZWRWMTZWRWO
      TZUAZIGUBZTZUCZABCUDZHESPUEWJWKWLXGTZWJWKXGUFNZXHGUFNZXJXIWJABCGGXDUFUFXG
      XFWNWPOXEOABCWNWPXEUGUHUIWJHUJZUFGHDUKJULZXLUMWJWKXIXHUNZHUFNZEUFNZXMWJWK
      XNXOXIXHKLMHEUFUFWMKRZUJZNZWOXQNZWQWSLRZNZXAXBUAZIXQUBZTZUCZABCUDZXGPWNWP
      WQYBIGUBZTZUCZABCUDUFXPHTZYEYIABCYJXRWNXSWPYDYHYJXQGWMYJXQXKGXPHUOJUPZUQY
      JXQGWOYKUQYJYCYGWQYJXQGTYCYGTYKYBIXQGURUSUTVAVBXTETZYIXFABCYLYHXEWNWPYLYG
      XDWQYLYBXCIGYLWRGNZOZYAWTXAXAXBXBYLYAWTVCYMXTEWSVDVEYNXAVFYNXBVFVGVHUTVIV
      BPMRYFTZKLMUDZYPUFUFVJVKZXPUFNXTUFNOYOOKLMUDABCMILKVLYPVMYPVNVMYQYPTYOKLM
      VOYOKLMVPYPVQVRYOKLMUFUFVSVTWAWBHDWCEFWCWDWEWFWGHEPWHWI $.
      $( [24-Apr-2011] $)
  $}

  ${
    $d B a b c p $.  $d L a b c $.  $d P a b c p $.  $d Q a b c p $.
    $d R a b c p $.
    isseg2.1 $e |- P = U. L $.
    $( The segment ` <. Q , R >. ` . $)
    isseg2 $p |- ( ( L e. A /\ B e. C /\ ( Q e. P /\ R e. P ) ) ->
      ( Q ( seg ` <. L , B >. ) R ) =
        { p e. P | ( <. <. Q , p >. , R >. e. B \/ p = Q \/ p = R ) } ) $=
      ( va vb vc wcel wa cop cseg cfv co cv wceq w3o crab w3a copab2 wi isseg1
      opreq eqeq1d imbi2d cvv opeq1 opeq1d eleq1d eqeq2 biidd 3orbi123d
      rabbidv opeq2 df-3an oprabbii oprabval2g 3expia cuni uniexg syl5eqel
      rabexg syl adantr syl5com syl5bir mpcom 3impia ) GAMZBCMZEDMZFDMZNZEFGBOP
      QZRZEHSZOZFOZBMZVTETZVTFTZUAZHDUBZTZVRJSZDMZKSZDMZLSWIVTOZWKOZBMZVTWITZVT
      WKTZUAZHDUBZTZUCZJKLUDZTZVMVNNZVQWHUEZJKLABCDGHIUFXCXEVQEFXBRZWGTZUEXDXCW
      HXGVQXCVSXFWGEFVRXBUGUHUIVQWGUJMZXGXDVOVPXHXGJKLEFDDWSWGXBWAWKOZBMZWDWQUA
      ZHDUBUJWIETZWRXKHDXLWOXJWPWDWQWQXLWNXIBXLWMWAWKWIEVTUKULUMWIEVTUNXLWQUOUP
      UQWKFTZXKWFHDXMXJWCWDWDWQWEXMXIWBBWKFWAURUMXMWDUOWKFVTUNUPUQXAWJWLNWTNJKL
      WJWLWTUSUTVAVBVMXHVNVMDUJMXHVMGVCUJDGAVDIVEWFHDUJVFVGVHVIVJVKVL $.
      $( [24-Apr-2011] $)
  $}


  ${
    $d x y $.
    $( A planar incidence betweenness geometry is a set equipped with an
       incidence relation and a betweenness relation.  The betweenness relation
       is a relation between three points of the plane.  Aitken's axiom B-0.
       (axiom B-0 is not described but implied by Aitken's system.)
       Experimental. $)
    df-plibg0 $a |- Plibg0 = { <. x , y >. | ( ( x e. Plig /\
         Rel y /\ Rel dom y ) /\ ( dom dom y = U. x /\ ran dom y = U. x /\
         ran y = U. x ) ) } $.
  $}

  ${
    $d B x y $.  $d L x y $.  $d P x y $.
    isplibg0.1 $e |- P = U. L $.
    $( The predicate "respects Aitken's axiom B-0 of an incidence-betweenness
       geometry ".  See ~ df-plibg0 . $)
    isplibg0 $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg0 <->
       ( ( L e. Plig /\ Rel B /\ Rel dom B ) /\ ( dom dom B = P /\
       ran dom B = P /\ ran B = P ) ) ) ) $=
      ( vx vy wcel wa cop cplibg0 cv cplig wrel cdm w3a cuni wceq crn copab
      df-plibg0 a1i eleq2d eleq1 3anbi1d unieq eqeq2d 3anbi123d anbi12d releq
      wb dmeq syl 3anbi23d dmeqd eqcomi eqeq12d rneqd rneq opelopabg bitrd ) EA
      IBCIJZEBKZLIVDGMZNIZHMZOZVGPZOZQZVIPZVERZSZVITZVMSZVGTZVMSZQZJZGHUAZIENIZ
      BOZBPZOZQZWDPZDSZWDTZDSZBTZDSZQZJZVCLWAVDLWASVCGHUBUCUDVTWBVHVJQZVLERZSZV
      OWPSZVQWPSZQZJWNGHEBACVEESZVKWOVSWTXAVFWBVHVJVEENUEUFXAVNWQVPWRVRWSXAVMWP
      VLVEEUGZUHXAVMWPVOXBUHXAVMWPVQXBUHUIUJVGBSZWOWFWTWMXCVHWCVJWEWBVGBUKXCVIW
      DSVJWEULVGBUMZVIWDUKUNUOXCWQWHWRWJWSWLXCVLWGWPDXCVIWDXDUPWPDSXCDWPFUQUCZU
      RXCVOWIWPDXCVIWDXDUSXEURXCVQWKWPDVGBUTXEURUIUJVAVB $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d x y p q r l $.
    $( Let ` p ` , ` q ` , ` r ` be three points.  If ` q ` is between ` p `
       and ` r ` then ` q ` is between ` r ` and ` p ` and ` p ` , ` q ` ,
       ` r ` are colinear and distinct.  Aitken's axiom B-1.  Experimental. $)
    df-plibg1 $a |- Plibg1 = { <. x , y >. |
        A. p e. U. x A. q e. U. x A. r e. U. x
           ( <. <. p , q >. , r >. e. y ->
               ( <. <. r , q >. , p >. e. y
                 /\ E. l e. x ( p e. l /\ q e. l /\ r e. l )
                 /\ ( p =/= q /\ p =/= r /\ q =/= r ) ) ) } $.
  $}

  ${
    $d B p q r x y $.  $d L l p q r x y $.  $d P p q r x y $.
    isplibg1.1 $e |- P = U. L $.
    $( The predicate "respects Aitken's axiom B-1 of a planar
       incidence-betweenness geometry ".  See ~ df-plibg1 . $)
    isplibg1 $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg1 <->
       A. p e. P A. q e. P A. r e. P ( <. <. p , q >. , r >. e. B ->
          ( <. <. r , q >. , p >. e. B
            /\ E. l e. L ( p e. l /\ q e. l /\ r e. l )
            /\ ( p =/= q /\ p =/= r /\ q =/= r ) ) ) ) ) $=
      ( vy vx wcel wa cop cplibg1 cv w3a wrex wne wi cuni wral copab wceq
      df-plibg1 a1i eleq2d unieq id rexeqdv 3anbi2d imbi2d raleqbidv eqcomi
      eleq2 3anbi1d imbi12d opelopabg bitrd ) EAMBCMNZEBOZPMVBHQZGQZOFQZOZKQZMZ
      VEVDOVCOZVGMZVCIQZMVDVKMVEVKMRZILQZSZVCVDTVCVETVDVETRZRZUAZFVMUBZUCZGVRUC
      ZHVRUCZLKUDZMVFBMZVIBMZVLIESZVORZUAZFDUCZGDUCZHDUCZVAPWBVBPWBUEVALKFGHIUF
      UGUHWAVHVJWEVORZUAZFEUBZUCZGWMUCZHWMUCWJLKEBACVMEUEZVTWOHVRWMVMEUIZWPVSWN
      GVRWMWQWPVQWLFVRWMWQWPVPWKVHWPVNWEVJVOWPVLIVMEWPUJUKULUMUNUNUNVGBUEZWOWIH
      WMDWMDUEWRDWMJUOUGZWRWNWHGWMDWSWRWLWGFWMDWSWRVHWCWKWFVGBVFUPWRVJWDWEVOVGB
      VIUPUQURUNUNUNUSUT $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d x y a b c d e $.
    $( For any two distinct points ` b ` and ` d ` there exists three points
       ` a ` , ` c ` , ` e ` such that ` b ` is between ` a ` and ` d ` , ` c `
       is between ` b ` and ` d ` , ` d ` is between ` b ` and ` e ` .
       Aitken's axiom B-2.  Experimental. $)
    df-plibg2 $a |- Plibg2 = { <. x , y >. |
      A. b e. U. x A. d e. U. x ( b =/= d ->
      E. a e. U. x E. c e. U. x E. e e. U. x
             ( <. <. a , b >. , d >. e. y
               /\ <. <. b , c >. , d >. e. y
               /\ <. <. b , d >. , e >. e. y ) ) } $.
  $}

  ${
    $d B a b c d e x y $.  $d L a b c d e x y $.  $d P a b c d e x y $.
    isplibg2.1 $e |- P = U. L $.
    $( The predicate "respects Aitken's axiom B-2 of a planar
       incidence-betweenness geometry ".  See ~ df-plibg2 . $)
    isplibg2 $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg2 <->
      A. b e. P A. d e. P ( b =/= d ->
      E. a e. P E. c e. P E. e e. P
             ( <. <. a , b >. , d >. e. B
               /\ <. <. b , c >. , d >. e. B
               /\ <. <. b , d >. , e >. e. B ) ) ) ) $=
      ( vy vx wcel wa cop cplibg2 cv wne w3a cuni wrex wi wral copab wceq
      df-plibg2 a1i eleq2d unieq anbi1d rexbidv2 anbi12d imbi2d imbi12d
      ralbidv2 eqcomi eleq2 3anbi123d rexeqbidv raleqbidv opelopabg bitrd ) FAN
      BCNOZFBPZQNVEHRZJRZSZGRZVFPVGPZLRZNZVFIRZPVGPZVKNZVFVGPERZPZVKNZTZEMRZUAZ
      UBZIWAUBZGWAUBZUCZJWAUDZHWAUDZMLUEZNVHVJBNZVNBNZVQBNZTZEDUBZIDUBZGDUBZUCZ
      JDUDZHDUDZVDQWHVEQWHUFVDMLEGHIJUGUHUIWGVHVSEFUAZUBZIWSUBZGWSUBZUCZJWSUDZH
      WSUDWRMLFBACVTFUFZWFXDHWAWSXEVFWANVFWSNWFXDXEWAWSVFVTFUJZUIXEWEXCJWAWSXEV
      GWANVGWSNWEXCXEWAWSVGXFUIXEWDXBVHXEWCXAGWAWSXEVIWANVIWSNWCXAXEWAWSVIXFUIX
      EWBWTIWAWSXEVMWANVMWSNWBWTXEWAWSVMXFUIXEVSVSEWAWSXEVPWANVPWSNVSXEWAWSVPXF
      UIUKULUMULUMULUNUOUPUOUPVKBUFZXDWQHWSDWSDUFXGDWSKUQUHZXGXCWPJWSDXHXGXBWOV
      HXGXAWNGWSDXHXGWTWMIWSDXHXGVSWLEWSDXHXGVLWIVOWJVRWKVKBVJURVKBVNURVKBVQURU
      SUTUTUTUNVAVAVBVC $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d x y l p q r $.
    $( If ` p ` , ` q ` , ` r ` are three distinct colinear points then ` p `
       is between ` q ` and ` r ` or ` q ` is between ` p ` and ` r ` or ` r `
       is between ` p ` and ` q ` .  Aitken's axiom B-3.  Experimental. $)
    df-plibg3 $a |- Plibg3 = { <. x , y >. |
        A. l e. x A. p e. U. x A. q e. U. x A. r e. U. x
           ( ( ( p e. l /\ q e. l /\ r e. l ) /\
             ( p =/= q /\ q =/= r /\ p =/= r ) ) ->
             ( ( <. <. q , p >. , r >. e. y /\
                 -. <. <. p , q >. , r >. e. y /\
                 -. <. <. p , r >. , q >. e. y ) \/
               ( -. <. <. q , p >. , r >. e. y /\
                    <. <. p , q >. , r >. e. y /\
                 -. <. <. p , r >. , q >. e. y ) \/
               ( -. <. <. q , p >. , r >. e. y /\
                 -. <. <. p , q >. , r >. e. y /\
                    <. <. p , r >. , q >. e. y ) ) ) } $.
  $}

  ${
    $d B l p q r x y $.  $d L l p q r x y $.  $d P p q r x y $.
    isplibg3.1 $e |- P = U. L $.
    $( The predicate "repects Aitken's axiom B-3 of a linear
       incidence-betweenness geometry ".  See ~ df-plibg3 . $)
    isplibg3 $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg3 <->
        A. l e. L A. p e. P A. q e. P A. r e. P
           ( ( ( p e. l /\ q e. l /\ r e. l ) /\
             ( p =/= q /\ q =/= r /\ p =/= r ) ) ->
             ( ( <. <. q , p >. , r >. e. B /\
                 -. <. <. p , q >. , r >. e. B /\
                 -. <. <. p , r >. , q >. e. B ) \/
               ( -. <. <. q , p >. , r >. e. B /\
                    <. <. p , q >. , r >. e. B /\
                 -. <. <. p , r >. , q >. e. B ) \/
               ( -. <. <. q , p >. , r >. e. B /\
                 -. <. <. p , q >. , r >. e. B /\
                    <. <. p , r >. , q >. e. B ) ) ) ) ) $=
      ( vy vx wcel wa cop cplibg3 cv w3a wne wn w3o wi cuni wral copab wceq
      df-plibg3 a1i eleq2d id unieq raleqdv raleqbidv eqcomi eleq2 notbid
      3anbi123d 3orbi123d imbi2d ralbidv opelopabg bitrd ) EAMBCMNZEBOZPMVDHQZI
      QZMGQZVFMFQZVFMRVEVGSVGVHSVEVHSRNZVGVEOVHOZKQZMZVEVGOVHOZVKMZTZVEVHOVGOZV
      KMZTZRZVLTZVNVRRZVTVOVQRZUAZUBZFLQZUCZUDZGWFUDZHWFUDZIWEUDZLKUEZMVIVJBMZV
      MBMZTZVPBMZTZRZWLTZWMWPRZWRWNWORZUAZUBZFDUDZGDUDZHDUDZIEUDZVCPWKVDPWKUFVC
      LKFGHIUGUHUIWJWDFEUCZUDZGXGUDZHXGUDZIEUDXFLKEBACWEEUFZWIXJIWEEXKUJXKWHXIH
      WFXGWEEUKZXKWGXHGWFXGXLXKWDFWFXGXLULUMUMUMVKBUFZXJXEIEXMXIXDHXGDXGDUFXMDX
      GJUNUHZXMXHXCGXGDXNXMWDXBFXGDXNXMWCXAVIXMVSWQWAWSWBWTXMVLWLVOWNVRWPVKBVJU
      OZXMVNWMVKBVMUOZUPZXMVQWOVKBVPUOZUPZUQXMVTWRVNWMVRWPXMVLWLXOUPZXPXSUQXMVT
      WRVOWNVQWOXTXQXRUQURUSUMUMUMUTVAVB $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d x y l p q r a $.
    $( Let ` l ` be a line.  Let ` p ` , ` q ` , ` r ` be three points not on
       the line ` l ` .  If the segments ` p ` ` q ` and ` q ` ` r ` don't
       intersect the line ` l ` then the segment ` p ` ` r ` doesn't intersect
       the line ` l ` .  Aitken's axiom B-4 (1st part).  Experimental. $)
    df-plibg4a $a |- Plibg4a = { <. x , y >. |
      A. l e. x A. p e. U. x A. q e. U. x A. r e. U. x
       ( ( ( -. p e. l /\ -. q e. l /\ -. r e. l ) /\
           ( { a e. U. x | ( <. <. p , a >. , q >. e. y \/ a = p \/ a = q ) }
                i^i l ) = (/) /\
           ( { a e. U. x | ( <. <. q , a >. , r >. e. y \/ a = q \/ a = r ) }
                 i^i l ) = (/) )
       -> ( { a e. U. x | ( <. <. p , a >. , r >. e. y \/ a = p \/ a = r ) }
                 i^i l ) = (/) ) } $.
  $}

  ${
    $d B a l p q r x y $.  $d L a l p q r x y $.  $d P a p q r x y $.
    isplibg4a.1 $e |- P = U. L $.
    $( The predicate " respects Aitken's axiom B-4 (first part) of an
       incidence-betweenness geometry ".  See ~ df-plibg4a . $)
    isplibg4a $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg4a <->
      A. l e. L A. p e. P A. q e. P A. r e. P
       ( ( ( -. p e. l /\ -. q e. l /\ -. r e. l ) /\
           ( { a e. P | ( <. <. p , a >. , q >. e. B \/ a = p \/ a = q ) }
                i^i l ) = (/) /\
           ( { a e. P | ( <. <. q , a >. , r >. e. B \/ a = q \/ a = r ) }
                 i^i l ) = (/) )
       -> ( { a e. P | ( <. <. p , a >. , r >. e. B \/ a = p \/ a = r ) }
                 i^i l ) = (/) ) ) ) $=
      ( vy vx wcel wa cop cplibg4a cv wn w3a wceq w3o cuni crab cin c0 wi wral
      copab df-plibg4a a1i eleq2d id unieq rabeq syl ineq1d eqeq1d 3anbi23d
      imbi12d raleqbidv eqcomi eleq2 biidd 3orbi123d rabbidv ax-mp syl5eq
      eqtrd ralbidv opelopabg bitrd ) EANBCNOZEBPZQNVNHRZJRZNSGRZVPNSFRZVPNSTZV
      OIRZPZVQPZLRZNZVTVOUAZVTVQUAZUBZIMRZUCZUDZVPUEZUFUAZVQVTPVRPZWCNZWFVTVRUA
      ZUBZIWIUDZVPUEZUFUAZTZWAVRPZWCNZWEWOUBZIWIUDZVPUEZUFUAZUGZFWIUHZGWIUHZHWI
      UHZJWHUHZMLUIZNVSWBBNZWEWFUBZIDUDZVPUEZUFUAZWMBNZWFWOUBZIDUDZVPUEZUFUAZTZ
      XABNZWEWOUBZIDUDZVPUEZUFUAZUGZFDUHZGDUHZHDUHZJEUHZVMQXLVNQXLUAVMMLFGHIJUJ
      UKULXKVSWGIEUCZUDZVPUEZUFUAZWPIYNUDZVPUEZUFUAZTZXCIYNUDZVPUEZUFUAZUGZFYNU
      HZGYNUHZHYNUHZJEUHYMMLEBACWHEUAZXJUUHJWHEUUIUMUUIXIUUGHWIYNWHEUNZUUIXHUUF
      GWIYNUUJUUIXGUUEFWIYNUUJUUIWTUUAXFUUDUUIWLYQWSYTVSUUIWKYPUFUUIWJYOVPUUIWI
      YNUAZWJYOUAUUJWGIWIYNUOUPUQURUUIWRYSUFUUIWQYRVPUUIUUKWQYRUAUUJWPIWIYNUOUP
      UQURUSUUIXEUUCUFUUIXDUUBVPUUIUUKXDUUBUAUUJXCIWIYNUOUPUQURUTVAVAVAVAWCBUAZ
      UUHYLJEUULUUGYKHYNDYNDUAZUULDYNKVBZUKZUULUUFYJGYNDUUOUULUUEYIFYNDUUOUULUU
      AYCUUDYHUULYQXQYTYBVSUULYPXPUFUULYOXOVPUULWGIDUDZXOYOUULWGXNIDUULWDXMWEWE
      WFWFWCBWBVCUULWEVDZUULWFVDZVEVFUUMYOUUPUAUUNWGIYNDUOVGVHUQURUULYSYAUFUULY
      SWPIDUDZVPUEYAUULYRUUSVPYRUUSUAZUULUUMUUTUUNWPIYNDUOVGUKUQUULUUSXTVPUULWP
      XSIDUULWNXRWFWFWOWOWCBWMVCUURUULWOVDZVEVFUQVIURUSUULUUCYGUFUULUUBYFVPUULX
      CIDUDZYFUUBUULXCYEIDUULXBYDWEWEWOWOWCBXAVCUUQUVAVEVFUUMUUBUVBUAUUNXCIYNDU
      OVGVHUQURUTVAVAVAVJVKVL $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d x y l p q r a $.
    $( Aitken's axiom B-4 (2nd part).  Let ` l ` be a line.  Let ` p ` ,
       ` q ` , ` r ` be three points not on the line ` l ` .  If the segments
       ` p ` ` q ` and ` q ` ` r ` intersect ` l ` then the segment ` p ` ` r `
       doesn't intersect ` l ` .  Experimental. $)
    df-plibg4b $a |- Plibg4b = { <. x , y >. |
      A. l e. x A. p e. U. x A. q e. U. x A. r e. U. x
       ( ( ( -. p e. l /\ -. q e. l /\ -. r e. l ) /\
         ( ( { a e. U. x | ( <. <. p , a >. , q >. e. y
             \/ a = p \/ a = q ) } i^i l ) =/= (/) /\
         ( { a e. U. x | ( <. <. q , a >. , r >. e. y
             \/ a = q \/ a = r ) } i^i l ) =/= (/) ) )
       -> ( { a e. U. x | ( <. <. p , a >. , r >. e. y
             \/ a = p \/ a = r ) } i^i l ) = (/) ) } $.
  $}

  ${
    $d B a l p q r x y $.  $d L a l p q r x y $.  $d P a p q r x y $.
    isplibg4b.1 $e |- P = U. L $.
    $( The predicate "respects Aitken's axiom B-4 (second part) of an
       incidence-betweenness geometry ".  See ~ df-plibg4b . $)
    isplibg4b $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg4b <->
      A. l e. L A. p e. P A. q e. P A. r e. P
       ( ( ( -. p e. l /\ -. q e. l /\ -. r e. l ) /\
         ( ( { a e. P | ( <. <. p , a >. , q >. e. B
             \/ a = p \/ a = q ) } i^i l ) =/= (/) /\
         ( { a e. P | ( <. <. q , a >. , r >. e. B
             \/ a = q \/ a = r ) } i^i l ) =/= (/) ) )
       -> ( { a e. P | ( <. <. p , a >. , r >. e. B
             \/ a = p \/ a = r ) } i^i l ) = (/) ) ) ) $=
      ( vy vx wcel wa cop cplibg4b cv wn w3a wceq w3o cuni crab cin c0 wne wi
      wral copab df-plibg4b a1i eleq2d id unieq rabeq syl ineq1d neeq1d
      anbi12d anbi2d eqeq1d imbi12d raleqbidv eqcomi eleq2 biidd 3orbi123d
      rabbidv ax-mp syl5eq ralbidv opelopabg bitrd ) EANBCNOZEBPZQNVPHRZJRZNSGR
      ZVRNSFRZVRNSTZVQIRZPZVSPZLRZNZWBVQUAZWBVSUAZUBZIMRZUCZUDZVRUEZUFUGZVSWBPV
      TPZWENZWHWBVTUAZUBZIWKUDZVRUEZUFUGZOZOZWCVTPZWENZWGWQUBZIWKUDZVRUEZUFUAZU
      HZFWKUIZGWKUIZHWKUIZJWJUIZMLUJZNWAWDBNZWGWHUBZIDUDZVRUEZUFUGZWOBNZWHWQUBZ
      IDUDZVRUEZUFUGZOZOZXDBNZWGWQUBZIDUDZVRUEZUFUAZUHZFDUIZGDUIZHDUIZJEUIZVOQX
      OVPQXOUAVOMLFGHIJUKULUMXNWAWIIEUCZUDZVRUEZUFUGZWRIYRUDZVRUEZUFUGZOZOZXFIY
      RUDZVRUEZUFUAZUHZFYRUIZGYRUIZHYRUIZJEUIYQMLEBACWJEUAZXMUUMJWJEUUNUNUUNXLU
      ULHWKYRWJEUOZUUNXKUUKGWKYRUUOUUNXJUUJFWKYRUUOUUNXCUUFXIUUIUUNXBUUEWAUUNWN
      UUAXAUUDUUNWMYTUFUUNWLYSVRUUNWKYRUAZWLYSUAUUOWIIWKYRUPUQURUSUUNWTUUCUFUUN
      WSUUBVRUUNUUPWSUUBUAUUOWRIWKYRUPUQURUSUTVAUUNXHUUHUFUUNXGUUGVRUUNUUPXGUUG
      UAUUOXFIWKYRUPUQURVBVCVDVDVDVDWEBUAZUUMYPJEUUQUULYOHYRDYRDUAZUUQDYRKVEZUL
      ZUUQUUKYNGYRDUUTUUQUUJYMFYRDUUTUUQUUFYGUUIYLUUQUUEYFWAUUQUUAXTUUDYEUUQYTX
      SUFUUQYSXRVRUUQWIIDUDZXRYSUUQWIXQIDUUQWFXPWGWGWHWHWEBWDVFUUQWGVGZUUQWHVGZ
      VHVIUURYSUVAUAUUSWIIYRDUPVJVKURUSUUQUUCYDUFUUQUUBYCVRUUQWRIDUDZYCUUBUUQWR
      YBIDUUQWPYAWHWHWQWQWEBWOVFUVCUUQWQVGZVHVIUURUUBUVDUAUUSWRIYRDUPVJVKURUSUT
      VAUUQUUHYKUFUUQUUGYJVRUUQXFIDUDZYJUUGUUQXFYIIDUUQXEYHWGWGWQWQWEBXDVFUVBUV
      EVHVIUURUUGUVFUAUUSXFIYRDUPVJVKURVBVCVDVDVDVLVMVN $.
      $( [23-Apr-2011] $)
  $}


  $( A planar incidence betweeness geometry respects the axioms ~ df-plibg0 to
     ~ df-plibg4b .  Experimental. $)
  df-plibg $a |- Plibg = ( Plibg0 i^i ( Plibg1 i^i ( Plibg2 i^i ( Plibg3
                           i^i ( Plibg4a i^i Plibg4b ) ) ) ) ) $.

  ${
    $d B a b c d e $.  $d B a l p q r $.  $d L a b c d e $.  $d L a l p q r $.
    $d P a b c d e $.  $d P a p q r $.
    isplibg.1 $e |- P = U. L $.
    isplibg.2 $e |- ( ph <-> ( ( L e. Plig /\ Rel B /\ Rel dom B ) /\
       ( dom dom B = P /\ ran dom B = P /\ ran B = P ) ) ) $.
    isplibg.3 $e |- ( ps <-> A. p e. P A. q e. P A. r e. P
       ( <. <. p , q >. , r >. e. B ->
          ( <. <. r , q >. , p >. e. B
            /\ E. l e. L ( p e. l /\ q e. l /\ r e. l )
            /\ ( p =/= q /\ p =/= r /\ q =/= r ) ) ) ) $.
    isplibg.4 $e |- ( ch <-> A. b e. P A. d e. P ( b =/= d ->
      E. a e. P E. c e. P E. e e. P
             ( <. <. a , b >. , d >. e. B
               /\ <. <. b , c >. , d >. e. B
               /\ <. <. b , d >. , e >. e. B ) ) ) $.
    isplibg.5 $e |- ( th <-> A. l e. L A. p e. P A. q e. P A. r e. P
           ( ( ( p e. l /\ q e. l /\ r e. l ) /\
             ( p =/= q /\ q =/= r /\ p =/= r ) ) ->
             ( ( <. <. q , p >. , r >. e. B /\
                 -. <. <. p , q >. , r >. e. B /\
                 -. <. <. p , r >. , q >. e. B ) \/
               ( -. <. <. q , p >. , r >. e. B /\
                    <. <. p , q >. , r >. e. B /\
                 -. <. <. p , r >. , q >. e. B ) \/
               ( -. <. <. q , p >. , r >. e. B /\
                 -. <. <. p , q >. , r >. e. B /\
                    <. <. p , r >. , q >. e. B ) ) ) ) $.
    isplibg.6 $e |- ( ta <-> A. l e. L A. p e. P A. q e. P A. r e. P
       ( ( ( -. p e. l /\ -. q e. l /\ -. r e. l ) /\
           ( { a e. P | ( <. <. p , a >. , q >. e. B \/ a = p \/ a = q ) }
                i^i l ) = (/) /\
           ( { a e. P | ( <. <. q , a >. , r >. e. B \/ a = q \/ a = r ) }
                 i^i l ) = (/) )
       -> ( { a e. P | ( <. <. p , a >. , r >. e. B \/ a = p \/ a = r ) }
                 i^i l ) = (/) ) ) $.
    isplibg.7 $e |- ( et <-> A. l e. L A. p e. P A. q e. P A. r e. P
       ( ( ( -. p e. l /\ -. q e. l /\ -. r e. l ) /\
         ( ( { a e. P | ( <. <. p , a >. , q >. e. B
             \/ a = p \/ a = q ) } i^i l ) =/= (/) /\
         ( { a e. P | ( <. <. q , a >. , r >. e. B
             \/ a = q \/ a = r ) } i^i l ) =/= (/) ) )
       -> ( { a e. P | ( <. <. p , a >. , r >. e. B
             \/ a = p \/ a = r ) } i^i l ) = (/) ) ) $.
    $( The predicate "is a planar incidence-betweenness geometry " $)
    isplibg $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg <->
       ( ph /\ ( ps /\ ( ch /\ ( th /\ ( ta /\ et ) ) ) ) ) ) ) $=
      ( wcel wa cop cplibg cplibg0 cplibg1 cplibg2 cplibg3 cplibg4a cplibg4b
      cin wceq df-plibg a1i eleq2d cplig wrel cdm w3a crn isplibg0 syl6bbr cv
      wrex wne wi wral isplibg1 isplibg2 wn w3o isplibg3 crab c0 isplibg4a
      isplibg4b anbi12d elin syl5bb bitrd ) LGUHHIUHUIZLHUJZUKUHWIULUMUNUOUPUQU
      RZURZURZURZURZUHZABCDEFUIZUIZUIZUIZUIZWHUKWNWIUKWNUSWHUTVAVBWHWIULUHZWIWM
      UHZUIWTWOWHXAAXBWSWHXALVCUHHVDHVEZVDVFXCVEJUSXCVGJUSHVGJUSVFUIAGHIJLUAVHU
      BVIWHWIUMUHZWIWLUHZUIWSXBWHXDBXEWRWHXDOVJZNVJZUJMVJZUJHUHZXHXGUJXFUJHUHXF
      TVJZUHZXGXJUHZXHXJUHZVFZTLVKXFXGVLZXFXHVLZXGXHVLZVFVFVMMJVNNJVNOJVNBGHIJL
      MNOTUAVOUCVIWHWIUNUHZWIWKUHZUIWRXEWHXRCXSWQWHXRQVJZSVJZVLPVJZXTUJYAUJHUHX
      TRVJUJYAUJHUHXTYAUJKVJUJHUHVFKJVKRJVKPJVKVMSJVNQJVNCGHIJKLPQRSUAVPUDVIWHW
      IUOUHZWIWJUHZUIWQXSWHYCDYDWPWHYCXNXOXQXPVFUIXGXFUJXHUJHUHZXIVQZXFXHUJXGUJ
      HUHZVQZVFYEVQZXIYHVFYIYFYGVFVRVMMJVNNJVNOJVNTLVNDGHIJLMNOTUAVSUEVIWHWIUPU
      HZWIUQUHZUIWPYDWHYJEYKFWHYJXKVQXLVQXMVQVFZXFYBUJZXGUJHUHYBXFUSZYBXGUSZVRP
      JVTXJURZWAUSXGYBUJXHUJHUHYOYBXHUSZVRPJVTXJURZWAUSVFYMXHUJHUHYNYQVRPJVTXJU
      RWAUSZVMMJVNNJVNOJVNTLVNEGHIJLMNOPTUAWBUFVIWHYKYLYPWAVLYRWAVLUIUIYSVMMJVN
      NJVNOJVNTLVNFGHIJLMNOPTUAWCUGVIWDWIUPUQWEWFWDWIUOWJWEWFWDWIUNWKWEWFWDWIUM
      WLWEWFWDWIULWMWEWFWG $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d B e t u v x $.  $d B f v w y z $.  $d L e t u v x $.  $d L f v w y z $.
    $d P e t u v x $.  $d P v w y z $.
    plibgax0.1 $e |- P = U. L $.
    $( The axiom zero of a planar incidence-betweenness geometry. $)
    plibgax0 $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg ->
    ( ( L e. Plig /\ Rel B /\ Rel dom B ) /\
       ( dom dom B = P /\ ran dom B = P /\ ran B = P ) ) ) ) $=
      ( vw vz vy vf vu ve vv vt vx wcel wa cop cplibg cplig wrel cdm w3a wceq
      crn cv wrex wne wi wral wn w3o crab cin c0 biid isplibg pm3.26bda ex ) EA
      PBCPQZEBRSPZETPBUABUBZUAUCVBUBDUDVBUEDUDBUEDUDUCQZUTVAVCGUFZHUFZRIUFZRBPZ
      VFVERVDRBPVDJUFZPZVEVHPZVFVHPZUCZJEUGVDVEUHZVDVFUHZVEVFUHZUCUCUIIDUJHDUJG
      DUJZKUFZLUFZUHMUFZVQRVRRBPVQNUFRVRRBPVQVRROUFRBPUCODUGNDUGMDUGUILDUJKDUJZ
      VLVMVOVNUCQVEVDRVFRBPZVGUKZVDVFRVERBPZUKZUCWAUKZVGWDUCWEWBWCUCULUIIDUJHDU
      JGDUJJEUJZVIUKVJUKVKUKUCZVDVSRZVERBPVSVDUDZVSVEUDZULMDUMVHUNZUOUDVEVSRVFR
      BPWJVSVFUDZULMDUMVHUNZUOUDUCWHVFRBPWIWLULMDUMVHUNUOUDZUIIDUJHDUJGDUJJEUJZ
      WGWKUOUHWMUOUHQQWNUIIDUJHDUJGDUJJEUJZQQQQVCVPVTWFWOWPABCDOEIHGMKNLJFVCUPV
      PUPVTUPWFUPWOUPWPUPUQURUS $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d B l p q r y $.  $d B v w x y z $.  $d L l p q r y $.  $d L v w x y z $.
    $d P p q r y $.  $d P v w x y z $.
    plibgax1.1 $e |- P = U. L $.
    $( The first axiom of a planar incidence-betweenness geometry. $)
    plibgax1 $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg ->
       A. p e. P A. q e. P A. r e. P
         ( <. <. p , q >. , r >. e. B ->
            ( <. <. r , q >. , p >. e. B
              /\ E. l e. L ( p e. l /\ q e. l /\ r e. l )
              /\ ( p =/= q /\ p =/= r /\ q =/= r ) ) ) ) ) $=
      ( vz vv vy vw vx wcel wa cop cplibg cv w3a wrex wne wi wral wn w3o wceq
      crab cin c0 cplig wrel cdm crn biid isplibg pm3.27bda pm3.26d ex ) EAPBCP
      QZEBRSPZHTZGTZRFTZRBPZVEVDRVCRBPVCITZPZVDVGPZVEVGPZUAZIEUBVCVDUCZVCVEUCZV
      DVEUCZUAUAUDFDUEGDUEHDUEZVAVBQVOKTZLTZUCMTZVPRVQRBPVPNTRVQRBPVPVQROTRBPUA
      ODUBNDUBMDUBUDLDUEKDUEZVKVLVNVMUAQVDVCRVERBPZVFUFZVCVERVDRBPZUFZUAVTUFZVF
      WCUAWDWAWBUAUGUDFDUEGDUEHDUEIEUEZVHUFVIUFVJUFUAZVCVRRZVDRBPVRVCUHZVRVDUHZ
      UGMDUIVGUJZUKUHVDVRRVERBPWIVRVEUHZUGMDUIVGUJZUKUHUAWGVERBPWHWKUGMDUIVGUJU
      KUHZUDFDUEGDUEHDUEIEUEZWFWJUKUCWLUKUCQQWMUDFDUEGDUEHDUEIEUEZQQQZVAVBEULPB
      UMBUNZUMUAWQUNDUHWQUODUHBUODUHUAQZVOWPQWRVOVSWEWNWOABCDOEFGHMKNLIJWRUPVOU
      PVSUPWEUPWNUPWOUPUQURUSUT $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d B a b c d e $.  $d B a w x y z $.  $d L a b c d e $.  $d L a w x y z $.
    $d P a b c d e $.  $d P a x y z $.
    plibgax2.1 $e |- P = U. L $.
    $( The second axiom of a planar incidence-betweenness geometry. $)
    plibgax2 $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg ->
      A. b e. P A. d e. P ( b =/= d ->
      E. a e. P E. c e. P E. e e. P
             ( <. <. a , b >. , d >. e. B
               /\ <. <. b , c >. , d >. e. B
               /\ <. <. b , d >. , e >. e. B ) ) ) ) $=
      ( vz vw vy vx wcel wa cop cplibg cv wne w3a wrex wi wral wn w3o wceq
      crab cin c0 cplig wrel cdm crn biid isplibg pm3.27bda pm3.27d pm3.26d ex
      ) FAPBCPQZFBRSPZHTZJTZUAGTZVDRVERBPVDITRVERBPVDVERETRBPUBEDUCIDUCGDUCUDJD
      UEHDUEZVBVCQZVGLTZMTZPZNTZVJPZOTZVJPZUBZVIVLUAZVLVNUAZVIVNUAZUBQVLVIRVNRB
      PZVIVLRVNRBPZUFZVIVNRVLRBPZUFZUBVTUFZWAWDUBWEWBWCUBUGUDODUENDUELDUEMFUEZV
      KUFVMUFVOUFUBZVIVFRZVLRBPVFVIUHZVFVLUHZUGGDUIVJUJZUKUHVLVFRVNRBPWJVFVNUHZ
      UGGDUIVJUJZUKUHUBWHVNRBPWIWLUGGDUIVJUJUKUHZUDODUENDUELDUEMFUEZWGWKUKUAWMU
      KUAQQWNUDODUENDUELDUEMFUEZQQZVHWAVNVLRVIRBPVPMFUCVQVSVRUBUBUDODUENDUELDUE
      ZVGWQQZVBVCFULPBUMBUNZUMUBWTUNDUHWTUODUHBUODUHUBQZWRWSQXAWRVGWFWOWPABCDEF
      ONLGHIJMKXAUPWRUPVGUPWFUPWOUPWPUPUQURUSUTVA $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d B l p q r y $.  $d B v w x y z $.  $d L l p q r y $.  $d L v w x y z $.
    $d P p q r y $.  $d P v w x y z $.
    plibgax3.1 $e |- P = U. L $.
    $( The third axiom of a planar incidence-betweenness geometry. $)
    plibgax3 $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg ->
      A. l e. L A. p e. P A. q e. P A. r e. P
           ( ( ( p e. l /\ q e. l /\ r e. l ) /\
             ( p =/= q /\ q =/= r /\ p =/= r ) ) ->
             ( ( <. <. q , p >. , r >. e. B /\
                 -. <. <. p , q >. , r >. e. B /\
                 -. <. <. p , r >. , q >. e. B ) \/
               ( -. <. <. q , p >. , r >. e. B /\
                    <. <. p , q >. , r >. e. B /\
                 -. <. <. p , r >. , q >. e. B ) \/
               ( -. <. <. q , p >. , r >. e. B /\
                 -. <. <. p , q >. , r >. e. B /\
                    <. <. p , r >. , q >. e. B ) ) ) ) ) $=
      ( vy vz vv vw vx wcel wa cop cplibg cv w3a wne wn w3o wi wral wceq crab
      cin c0 wrex cplig wrel cdm crn biid isplibg pm3.27bda pm3.27d pm3.26d ex
      ) EAPBCPQZEBRSPZHTZITZPZGTZVEPZFTZVEPZUAZVDVGUBZVGVIUBZVDVIUBZUAQVGVDRVIR
      BPZVDVGRVIRBPZUCZVDVIRVGRBPZUCZUAVOUCZVPVSUAVTVQVRUAUDUEFDUFGDUFHDUFIEUFZ
      VBVCQZWAVFUCVHUCVJUCUAZVDKTZRZVGRBPWDVDUGZWDVGUGZUDKDUHVEUIZUJUGVGWDRVIRB
      PWGWDVIUGZUDKDUHVEUIZUJUGUAWEVIRBPWFWIUDKDUHVEUIUJUGZUEFDUFGDUFHDUFIEUFZW
      CWHUJUBWJUJUBQQWKUEFDUFGDUFHDUFIEUFZQZWBLTZMTZUBWDWORWPRBPWONTRWPRBPWOWPR
      OTRBPUAODUKNDUKKDUKUEMDUFLDUFZWAWNQZWBVPVIVGRVDRBPVKIEUKVLVNVMUAUAUEFDUFG
      DUFHDUFZWQWRQZVBVCEULPBUMBUNZUMUAXAUNDUGXAUODUGBUODUGUAQZWSWTQXBWSWQWAWLW
      MABCDOEFGHKLNMIJXBUPWSUPWQUPWAUPWLUPWMUPUQURUSUSUTVA $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d B a l p q r $.  $d B a w x y z $.  $d L a l p q r $.  $d L a w x y z $.
    $d P a p q r $.  $d P a w x y z $.
    plibgax4a.1 $e |- P = U. L $.
    $( The fourth axiom of a planar incidence-betweenness geometry (first
       part). $)
    plibgax4a $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg ->
       A. l e. L A. p e. P A. q e. P A. r e. P
       ( ( ( -. p e. l /\ -. q e. l /\ -. r e. l ) /\
           ( { a e. P | ( <. <. p , a >. , q >. e. B \/ a = p \/ a = q ) }
                i^i l ) = (/) /\
           ( { a e. P | ( <. <. q , a >. , r >. e. B \/ a = q \/ a = r ) }
                 i^i l ) = (/) )
       -> ( { a e. P | ( <. <. p , a >. , r >. e. B \/ a = p \/ a = r ) }
                 i^i l ) = (/) ) ) ) $=
      ( vy vw vz vx wcel wa cop cplibg cv wn w3a wceq w3o crab cin c0 wi wral
      wne wrex cplig wrel cdm crn biid isplibg pm3.27bda pm3.27d pm3.26d ex )
      EAPBCPQZEBRSPZHTZJTZPZUAGTZVEPZUAFTZVEPZUAUBZVDITZRZVGRBPVLVDUCZVLVGUCZUD
      IDUEVEUFZUGUCVGVLRVIRBPVOVLVIUCZUDIDUEVEUFZUGUCUBVMVIRBPVNVQUDIDUEVEUFUGU
      CZUHFDUIGDUIHDUIJEUIZVBVCQZVTVKVPUGUJVRUGUJQQVSUHFDUIGDUIHDUIJEUIZWAVFVHV
      JUBZVDVGUJZVGVIUJZVDVIUJZUBQVGVDRVIRBPZVDVGRVIRBPZUAZVDVIRVGRBPZUAZUBWGUA
      ZWHWKUBWLWIWJUBUDUHFDUIGDUIHDUIJEUIZVTWBQZWALTZMTZUJVLWORWPRBPWONTRWPRBPW
      OWPROTRBPUBODUKNDUKIDUKUHMDUILDUIZWMWNQZWAWHVIVGRVDRBPWCJEUKWDWFWEUBUBUHF
      DUIGDUIHDUIZWQWRQZVBVCEULPBUMBUNZUMUBXAUNDUCXAUODUCBUODUCUBQZWSWTQXBWSWQW
      MVTWBABCDOEFGHILNMJKXBUPWSUPWQUPWMUPVTUPWBUPUQURUSUSUSUTVA $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d B a l p q r $.  $d B a w x y z $.  $d L a l p q r $.  $d L a w x y z $.
    $d P a p q r $.  $d P a w x y z $.
    plibgax4b.1 $e |- P = U. L $.
    $( The fourth axiom of a planar incidence-betweenness geometry (second
       part). $)
    plibgax4b $p |- ( ( L e. A /\ B e. C ) -> ( <. L , B >. e. Plibg ->
       A. l e. L A. p e. P A. q e. P A. r e. P
       ( ( ( -. p e. l /\ -. q e. l /\ -. r e. l ) /\
         ( ( { a e. P | ( <. <. p , a >. , q >. e. B
             \/ a = p \/ a = q ) } i^i l ) =/= (/) /\
         ( { a e. P | ( <. <. q , a >. , r >. e. B
             \/ a = q \/ a = r ) } i^i l ) =/= (/) ) )
       -> ( { a e. P | ( <. <. p , a >. , r >. e. B
             \/ a = p \/ a = r ) } i^i l ) = (/) ) ) ) $=
      ( vy vw vz vx wcel wa cop cplibg cv wn w3a wceq w3o crab cin c0 wne wi
      wral wrex cplig wrel cdm crn biid isplibg pm3.27bda pm3.27d ex ) EAPBCPQZ
      EBRSPZHTZJTZPZUAGTZVDPZUAFTZVDPZUAUBZVCITZRZVFRBPVKVCUCZVKVFUCZUDIDUEVDUF
      ZUGUHVFVKRVHRBPVNVKVHUCZUDIDUEVDUFZUGUHQQVLVHRBPVMVPUDIDUEVDUFUGUCZUIFDUJ
      GDUJHDUJJEUJZVAVBQZVJVOUGUCVQUGUCUBVRUIFDUJGDUJHDUJJEUJZVSVTVEVGVIUBZVCVF
      UHZVFVHUHZVCVHUHZUBQVFVCRVHRBPZVCVFRVHRBPZUAZVCVHRVFRBPZUAZUBWFUAZWGWJUBW
      KWHWIUBUDUIFDUJGDUJHDUJJEUJZWAVSQZVTLTZMTZUHVKWNRWORBPWNNTRWORBPWNWOROTRB
      PUBODUKNDUKIDUKUIMDUJLDUJZWLWMQZVTWGVHVFRVCRBPWBJEUKWCWEWDUBUBUIFDUJGDUJH
      DUJZWPWQQZVAVBEULPBUMBUNZUMUBWTUNDUCWTUODUCBUODUCUBQZWRWSQXAWRWPWLWAVSABC
      DOEFGHILNMJKXAUPWRUPWPUPWLUPWAUPVSUPUQURUSUSUSUSUT $.
      $( [23-Apr-2011] $)
  $}

