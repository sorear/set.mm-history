$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topological groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TopGrp $.

  $( Extend class notation with the class of all topological groups. $)
  ctopgrp $a class TopGrp $.

  ${
    $d g j $.
    $( Define the class of all topological groups.  A topological group is a
       group whose operation and inverse function are continuous. $)
    df-topgrp $a |- TopGrp = { <. g , j >. | ( ( g e. Grp /\ j e. Top ) /\
         g e. ( ( j tX j ) Cn j ) /\ ( inv ` g ) e. ( j Cn j ) ) } $.
  $}

  ${
    $d G g j $.  $d J g j $.  $d x y $.
    $( The predicate "is a topological group".  Bourbaki TG III.1 $)
    istopgrp $p |- ( J e. A -> ( <. G , J >. e. TopGrp <->
      ( ( G e. Grp /\ J e. Top ) /\ G e. ( ( J tX J ) Cn J )
        /\ ( inv ` G ) e. ( J Cn J ) ) ) ) $=
      ( vx vy vg vj wcel cop ctopgrp cgr ctop wa ctx co ccn cgn cfv w3a cvv
      wbr df-br wrel cv copab relopab df-topgrp releqi mpbir brrelexi sylbir
      adantl simpl jca elisset adantr 3ad2ant1 wceq eleq1 anbi1d fveq2 eleq1d
      3anbi123d anbi2d id opreq12d eleq2d opelopabg eleq2i syl5bb pm5.21nd ) CA
      HZBCIZJHZBKHZCLHZMZBCCNOZCPOZHZBQRZCCPOZHZSZBTHZVLMZVLVNMWEVLVNWEVLVNBCJU
      AWEBCJUBBCJJUCDUDZKHEUDZLHMWGWHWHNOWHPOHWGQRWHWHPOHSZDEUEZUCWIDEUFJWJDEUG
      UHUIUJUKULVLVNUMUNVLWDMWEVLWDWEVLVQVTWEWCVOWEVPBKUOUPUQULVLWDUMUNWFVMFUDZ
      KHZGUDZLHZMZWKWMWMNOZWMPOZHZWKQRZWMWMPOZHZSZFGUEZHWDVNXBVOWNMZBWQHZWAWTHZ
      SWDFGBCTAWKBURZWOXDWRXEXAXFXGWLVOWNWKBKUSUTWKBWQUSXGWSWAWTWKBQVAVBVCWMCUR
      ZXDVQXEVTXFWCXHWNVPVOWMCLUSVDXHWQVSBXHWPVRWMCPXHWMCWMCNXHVEZXIVFXIVFVGXHW
      TWBWAXHWMCWMCPXIXIVFVGVCVHJXCVMFGUGVIVJVK $.
      $( [18-Apr-2010] $)
  $}

  ${
    topgrpi.1 $e |- G = ( 1st ` K ) $.
    ${
      topgrpi.2 $e |- J = ( 2nd ` K ) $.
      ${
        $d G g j $.  $d J g j $.  $d K g j $.  $d N g j $.
        topgrpi.3 $e |- N = ( inv ` G ) $.
        $( "Axioms" of a topological group. $)
        topgrpi $p |- ( K e. TopGrp -> ( ( G e. Grp /\ J e. Top ) /\
          G e. ( ( J tX J ) Cn J ) /\ N e. ( J Cn J ) ) ) $=
          ( vg vj ctopgrp wcel cv cgr ctop wa ctx co ccn cgn cfv w3a copab
          df-topgrp eleq2i c1st wceq wb eqtr3 eqcomd eleq1 anbi1d fveq2
          syl6eqr eleq1d 3anbi123d syl mpan c2nd anbi2d opreq12 anidms mpancom
          eleq2d elopabi sylbi ) CJKCHLZMKZILZNKZOZVFVHVHPQZVHRQZKZVFSTZVHVHRQZ
          KZUAZHIUBZKAMKZBNKZOZABBPQZBRQZKZDBBRQZKZUAZJVRCHIUCUDVQVSVIOZAVLKZDV
          OKZUAZWGHICACUETZUFZVFWLUFZVQWKUGZEWMWNOZVFAUFZWOWPAVFAVFWLUHUIWQVJWH
          VMWIVPWJWQVGVSVIVFAMUJUKVFAVLUJWQVNDVOWQVNASTDVFASULGUMUNUOUPUQBCURTZ
          UFZVHWRUFZWKWGUGZFWSWTOZVHBUFZXAXBBVHBVHWRUHUIXCWHWAWIWDWJWFXCVIVTVSV
          HBNUJUSXCVLWCAVKWBUFZXCVLWCUFXCXDVHBVHBPUTVAVKWBVHBRUTVBVCXCVOWEDXCVO
          WEUFVHBVHBRUTVAVCUOUPUQVDVE $.
          $( [18-Apr-2010] $)

        $( In a topological group, the inverse function is continuous. $)
        topgrpinv $p |- ( K e. TopGrp -> N e. ( J Cn J ) ) $=
          ( ctopgrp wcel cgr ctop wa ctx co ccn topgrpi simp3d ) CHIAJIBKILABBM
          NBONIDBBONIABCDEFGPQ $.
          $( [21-Jun-2010] $)
      $}

      $( In a topological group, the base set of the topology and the base set
         of the group are equal. $)
      topgrpbs $p |- ( K e. TopGrp -> ran G = U. J ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv w3a crn cuni wceq eqid
        topgrpi wf1o wi grpinvf wf f1of cdm fdm eqtr2 ex syl5 syl cnf syl5com
        3exp pm2.43i com3r imp31 3adant2 ) CFGAHGZBIGZJZABBKLBMLGZANOZBBMLGZPAQ
        ZBRZSZABCVDDEVDTZUAVBVEVHVCUTVAVEVHUTVFVFVDUBZVAVEVHUCUCAVDVFVFTVIUDVAV
        EVJVHVAVEVJVHUCZUCVAVAVEVKVJVGVGVDUEZVHVAVAVEPVJVFVFVDUEZVLVHUCZVFVFVDU
        FVMVDUGZVFSZVNVFVFVDUHVPVOVGSZVHVLVPVQVHVOVFVGUIUJVGVGVDUHUKULULVDBBVGV
        GVGTZVRUMUNUOUPUQULURUSUL $.
        $( [21-Jun-2010] $)

      $( In a topological group, the operation ` G ` is continuous. $)
      topgrpcn $p |- ( K e. TopGrp -> G e. ( ( J tX J ) Cn J ) ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv eqid topgrpi simp2d ) CFG
        AHGBIGJABBKLBMLGANOZBBMLGABCSDESPQR $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the operation ` G ` is a group. $)
    topgrpgrp $p |- ( K e. TopGrp -> G e. Grp ) $=
      ( ctopgrp wcel cgr c2nd cfv ctop wa ctx co ccn cgn eqid topgrpi simp1d
      simpld ) BDEZAFEZBGHZIEZSTUBJAUAUAKLUAMLEANHZUAUAMLEAUABUCCUAOUCOPQR $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrptop.1 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, ` J ` is a topology. $)
    topgrptop $p |- ( K e. TopGrp -> J e. Top ) $=
      ( ctopgrp wcel c1st cfv cgr ctop wa ctx co ccn cgn eqid topgrpi simp1d
      simprd ) BDEZBFGZHEZAIEZSUAUBJTAAKLAMLETNGZAAMLETABUCTOCUCOPQR $.
      $( [21-Jun-2010] $)
  $}

  ${
    extopgrp.1 $e |- A e. B $.
    ${
      $d A x y $.
      $( The identity element of the trivial group. $)
      idtrgrp $p |- ( Id ` { <. <. A , A >. , A >. } ) = A $=
        ( vx vy cop csn cgi cfv cv co wceq wral crab cuni crn opex elisseti
        rnsnop eqcomi eqid grpsn grpidvallem wcel wa cab elsni adantr elsn
        biimpri copab fopabsn opreqi df-opr snid eqidd fvopab4 ax-mp 3eqtri
        opreq2 id 3eqtr4a syl rgen opreq1 eqeq1d ralbidv mpbiri jca impbii
        abbii df-rab df-sn 3eqtr4i unieqi unisn eqtri ) AAFZAFGZHIZDJZEJZVSKZWB
        LZEAGZMZDWENZOZAEDVTVSWEVSPWEVRAAAQZABCRZSTVTUAAWJUBUCWHWEOAWGWEWAWEUDZ
        WFUEZDUFWAALZDUFWGWEWLWMDWLWMWKWMWFWAAUGUHWMWKWFWKWMDAUIUJWMWFAWBVSKZWB
        LZEWEMWOEWEWBWEUDWBALZWOWBAUGWPAAVSKZAWNWBWQAAWAVRGZUDWPUEDEUKZKVRWSIZA
        VSWSAADEVRAWIWJULUMAAWSUNVRWRUDWTALVRWIUODEVRAAWRWSWAVRLAUPWSUAWJUQURUS
        WBAAVSUTWPVAVBVCVDWMWDWOEWEWMWCWNWBWAAWBVSVEVFVGVHVIVJVKWFDWEVLDAVMVNVO
        AWJVPVQVQ $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.
      $( The inverse function of the trivial group. $)
      invtrgrp $p |- ( inv ` { <. <. A , A >. , A >. } ) = ( _I |` { A } ) $=
        ( vx vy vz cop csn cgn cfv cv wcel co wceq crab cuni wa copab cid cres
        cgr elisseti grpsn crn opex rnsnop eqcomi cgi idtrgrp eqid grpinvfval
        ax-mp cxp restidsing fconstopab elsn simpl opreq2d eqeq1d rabbidva
        unieqd df-opr fvsn eqtri wreu wb snid weu wal wex elsni adantr biimpri
        opreq1 syl6eq jca impbii ax-gen eqeq2 bibi2d albidv cla4egv mp2 df-eu
        mpbir df-reu reuuni2 bicomd mp2an syl6req sylbi eqeq2d pm5.32i opabbii
        3eqtrri ) AAGZAGHZIJZDKZAHZLZEKZFKZWSWQMZANZFWTOZPZNZQZDERZSWTTZWQUALWR
        XJNAABCUBZUCDFAEWQWRWTWQUDWTWPAAAUEZXLUFUGWQUHJAABCUIUGWRUJUKULXKWTWTUM
        XAXBANZQZDERXJAUNDEWTAUOXOXIDEXAXNXHXAAXGXBXAWSANZAXGNDAUPXPXGXCAWQMZAN
        ZFWTOZPZAXPXFXSXPXEXRFWTXPXCWTLZQZXDXQAYBWSAXCWQXPYAUQURUSUTVAXTANZAAWQ
        MZANZYDWPWQJAAAWQVBWPAXMXLVCVDZAWTLZXRFWTVEZYCYEVFAXLVGYHYAXRQZFVHZYJYI
        XCXBNZVFZFVIZEVJZABLYIXCANZVFZFVIZYNCYPFYIYOYAYOXRXCAVKVLYOYAXRYAYOFAUP
        VMYOXQYDAXCAAWQVNZYFVOVPVQVRYMYQEABXNYLYPFXNYKYOYIXBAXCVSVTWAWBWCYIFEWD
        WEXRFWTWFWEYGYHQYEYCXRYEFWTAYOXQYDAYRUSWGWHWIWEWJWKWLWMWNWOVD $.
        $( [21-Jun-2010] $)
    $}

    $( The trivial topological group. $)
    extopgrp $p |- <. { <. <. A , A >. , A >. } , { (/) , { A } } >.
       e. TopGrp $=
      ( cop csn c0 cpr ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv cvv w3a wb
      prex istopgrp ax-mp elisseti grpsn indistop pm3.2i cxp wf eqid txtop
      mp2an wfo crn opex rnsnop eqcomi grpfo fof syl cuni wceq cun 0ex snex
      unipr uncom un0 3eqtrri txuni mapudiscn cid cres invtrgrp idcn eqeltri
      mpbir3an ) AADZADEZFAEZGZDHIZVSJIZWAKIZLZVSWAWAMNZWAONIZVSPQZWAWAONZIZWAR
      IWBWEWGWJSTFVTUARVSWAUBUCWCWDAABCUDZUEZVTUFZUGWFKIZVTVTUHZVTVSUIZWGWDWDWN
      WMWMWAWAWFWFUJZUKULWCWPWLWCWOVTVSUMWPVSVTVSUNVTVRAAAUOWKUPUQURWOVTVSUSUTU
      CWOVTVSWFWFVAZWOWDWDWRWOVBWMWMWAWAWFVTVTWQWAVAFVTVCVTFVCVTFVTVDAVEZVFFVTV
      GVTVHVIZWTVJULUQWSVKULWHVLVTVMZWIABCVNWDXAWIIWMWAVTWTVOUCVPVQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrpsubcn.1 $e |- G = ( 1st ` K ) $.
    topgrpsubcn.2 $e |- J = ( 2nd ` K ) $.
    topgrpsubcn.3 $e |- D = ( /g ` G ) $.
    ${
      $d G u x y z $.  $d J u x y $.
      topgrpsubcn.4 $e |- K e. TopGrp $.
      $( Lemma for ~ topgrpsubcn . $)
      topgrpsubcnlem $p |- D e. ( ( J tX J ) Cn J ) $=
        ( vx vy vz vu cv crn wcel wa cgn cfv co wceq copab2 ctx ccn ctopgrp
        cgr topgrpgrp eqid grpdivfval syl ax-mp cop ccom cxp wfn wfo grpfo
        fofn 3syl simpl wi grpinvcl ex adantl oprab2co ctop w3a topgrptop
        txtop syl11anc 3jca cid cres cuni topgrpbs idcn topgrpinv 2txcn
        syl222anc fvresi adantr eqcomd opeq1d eqeq2d pm5.32i oprabbii syl5eqel
        topgrpcn jca32 cnco eqeltri ) AIMZBNZOZJMZWLOZPZKMWKWNBQRZRZBSTPIJKUAZC
        CUBSZCUCSZDUDOZAWSTZHXBBUEOZXCBDEUFZIJKABWQWLWLUGZWQUGZGUHUIUJWSBWPLMZW
        KWRUKZTZPZIJLUAZULZXAXBWSXMTZHXBBWLWLUMZUNZXNXBXDXOWLBUOXPXEBWLXFUPXOWL
        BUQURIJLKWLWLWKWRWLWLXLWSBWMWOUSWOWRWLOZWMXBWOXQUTZHXBXDXRXEXDWOXQWNBWQ
        WLXFXGVAVBUIUJVCXLUGWSUGVDUIUJWTVEOZXSCVEOZVFZXLWTWTUCSZOZBXAOZPPZXMXAO
        XBYEHXBYAYCYDXBXSXSXTXBXTXTXSCDFVGZYFCCWTWTUGZVHVIZYHYFVJXBWPXHWKVKWLVL
        ZRZWRUKZTZPZIJLUAZYBXLXBXTXTXTXTYICCUCSZOZWQYOOYNYBOYFYFYFYFXBXTYPYFCWL
        XBWLCVMTHBCDEFVNUJZVOUIBCDWQEFXGVPIJLCCWTYIWQYNCCWTWLWLYGYGYQYQYNUGVQVR
        XKYMIJLWPXJYLWPXIYKXHWPWKYJWRWPYJWKWMYJWKTWOWLWKVSVTWAWBWCWDWEWFBCDEFWG
        WHUJXLBWTWTCWIUJWJWJ $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the "subtraction" (or "division") is
       continuous.  Bourbaki TG III.1 axiom GT' $)
    topgrpsubcn $p |- ( K e. TopGrp -> D e. ( ( J tX J ) Cn J ) ) $=
      ( ctopgrp wcel c1st cfv cgs ctx co ccn c2nd c0 cop csn cpr cif wceq
      fveq2 fveq2d opreq12d eleq12d eqid cvv 0ex extopgrp elimel
      topgrpsubcnlem dedth opreq12i syl6eleqr fveq2i eqtri syl5eqel ) DHIZDJKZL
      KZCCMNZCONZAUSVADPKZVDMNZVDONZVCUSVAVFIUSDQQRQRSQQSTRZUAZJKZLKZVHPKZVKMNZ
      VKONZIDVGDVHUBZVAVJVFVMVNUTVILDVHJUCUDVNVEVLVDVKOVNVDVKVDVKMDVHPUCZVOUEVO
      UEUFVJVIVKVHVIUGVKUGVJUGDVGHQUHUIUJUKULUMVBVECVDOCVDCVDMFFUNFUNUOABLKVAGB
      UTLEUPUQUR $.
      $( [21-Jun-2010] $)
  $}

  ${
    trhom.1 $e |- X = ran G $.
    trhom.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trhom.3 $e |- G = ( 1st ` K ) $.
    trhom.4 $e |- J = ( 2nd ` K ) $.
    ${
      $d A x y $.  $d G x y $.  $d K x y $.  $d X x y $.
      $( In a topological group a right translation is a homeomorphism.
         Bourbaki TG III.2 $)
      trhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
        ( vy ctopgrp wcel wa chomeosm co cuni wf1o ccn ccnv w3a crn wceq wi
        topgrpbs wb eqtr f1oeq2 f1oeq3 bitrd eqcoms imbi2d syl mpan cgr
        topgrpgrp trooo ex syl5bir mpcom imp c2nd cfv opreq12 anidms eleq2d cv
        cop copab ccom cxp wral wfn opelxpi ancoms r19.21aiva adantl wfo grpfo
        fofn 3syl adantr eqid cmpt df-mpt df-opr eqeq2i anbi2i opabbii 3eqtri
        fnopabco2b syl11anc ctop ctx topgrptop txtop 3jca eqcomi unieqi
        eqeq12i biimpi ttcn sylan eleq2 anbi2d ax-17 anbi1d opabbid eleq1d
        imbi12d mpbiri anabsi5 topgrpcn cnco syl12anc eqeltrd ax-mp cgs trinv
        grpdivfo eqtri topgrpsubcn cvv wf fex oprex a1i rgen fopab2a mpbi c1st
        fvex eqeltri rnex sylancr hmeobc syl111anc mpbird ) FMNZBGNZOZCEEPQNZER
        ZUUDCSZCEETQZNZCUAZUUFNZUBZUUBUUEUUGUUIYTUUAUUEDUCZUUDUDZYTUUAUUEUEZDEF
        JKUFZUULUUMUUAGGCSZUEZYTGUUKUDZUULUUMUUPUGZHUUQUULOGUUDUDZUURGUUKUUDUHU
        USUUEUUOUUAUUEUUOUGUUDGUUDGUDUUEGUUDCSUUOUUDGUUDCUIUUDGGCUJUKULUMUNUOYT
        DUPNZUUPDFJUQZUUTUUAUUOABCDGIHURUSUNUTVAVBEFVCVDZUDZUUBUUGUEKUVCUUGCUVB
        UVBTQZNUUBUVCUUFUVDCUVCUUFUVDUDEUVBEUVBTVEVFZVGUUBCDAVHZGNZLVHZUVFBVIZU
        DZOZALVJZVKZUVDUUBUVIGGVLZNZAGVMZDUVNVNZCUVMUDUUAUVPYTUUAUVOAGUVGUUAUVO
        UVFBGGVOVPVQVRZYTUVQUUAYTUUTUVNGDVSUVQUVADGHVTUVNGDWAWBWCALGUVIUVNUVLCD
        UVLWDZCAGUVFBDQZWEUVGUVHUVTUDZOZALVJUVGUVHUVIDVDZUDZOZALVJIALGUVTWFUWBU
        WEALUWAUWDUVGUVTUWCUVHUVFBDWGWHWIWJWKWLWMUUBUVBWNNZUVBUVBWOQZWNNZUWFUBZ
        UVLUVBUWGTQZNZDUWGUVBTQZNZUVMUVDNYTUWIUUAYTUWFUWHUWFUVBFUVBWDZWPZYTUWFU
        WFUWHUWOUWOUVBUVBUWGUWGWDWQWMUWOWRWCZYTUUAUWKYTUULGUVBRZUDZUUBUWKUEZUUN
        UULUWRUUKGUUDUWQGUUKHWSEUVBKWTXAXBUWRUWSYTBUWQNZOZUVFUWQNZUVJOZALVJZUWJ
        NZUEUWFUWTUXEYTALBUXDUVBUWQUWQWDUXDWDXCUWOXDUWRUUBUXAUWKUXEUWRUUAUWTYTG
        UWQBXEXFUWRUVLUXDUWJUWRUVKUXCALUWRAXGUWRLXGUWRUVGUXBUVJGUWQUVFXEXHXIXJX
        KXLWBXMZYTUWMUUADUVBFJUWNXNWCUVLDUVBUWGUVBXOXPXQUTXRUUBUUHAGUVFBDXSVDZQ
        ZWEZUUFUUTUUAUUHUXIUDYTABUXGCDGIHUXGWDZXTUVAXDUVCUUBUXIUUFNZUEKUVCUXKUX
        IUVDNUUBUVCUUFUVDUXIUVEVGUUBUXIUXGUVLVKZUVDUUBUVPUXGUVNVNZUXIUXLUDUVRYT
        UXMUUAYTUUTUVNGUXGVSUXMUVAUXGDGHUXJYAUVNGUXGWAWBWCALGUVIUVNUVLUXIUXGUVS
        UXIUVGUVHUXHUDZOZALVJUVGUVHUVIUXGVDZUDZOZALVJALGUXHWFUXOUXRALUXNUXQUVGU
        XHUXPUVHUVFBUXGWGWHWIWJYBWLWMUUBUWIUWKUXGUWLNZUXLUVDNUWPUXFYTUXSUUAUXGD
        UVBFJUWNUXJYCWCUVLUXGUVBUWGUVBXOXPXQUTXRXQWRUUBEWNNZUXTCYDNZUUCUUJUGYTU
        XTUUAEFKWPWCZUYBGYDCYEZGYDNZUYAUUBGYDYDCYFUVTYDNZAGVMUYCUYEAGUYEUVGUVFB
        DYGYHYIAGYDUVTCIYJYKUYDUUBGUUKYDHDDFYLVDYDJFYLYMYNYOYNYHYPYDCEEUUDUUDUU
        DWDZUYFYQYRYS $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.
      $( In a topological group, a converse of a right translation is a
         homeomorphism.  Bourbaki TG III.2 $)
      cnvtrhom $p |- ( ( K e. TopGrp /\ A e. X )
        -> `' F e. ( J Homeo J ) ) $=
        ( ctopgrp wcel wa ctop chomeosm co ccnv topgrptop jca adantr trhom
        cnvhmpha sylc ) FLMZBGMZNEOMZUGNZCEEPQZMCRUIMUEUHUFUEUGUGEFKSZUJTUAABCD
        EFGHIJKUBCEEUCUD $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d F y $.  $d G x $.  $d J y $.  $d K x $.  $d O y $.
      $d X x $.
      $( In a topological group the image of an open set by a right translation
         is an open set.  Bourbaki TG III.2 $)
      trhom2 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
       -> ( F " O ) e. J ) $=
        ( vy ctopgrp wcel cima chomeosm co wa wi trhom cv wral ctop topgrptop
        jca adantr hmeocnb syl wceq imaeq2 eleq1d rcla4cv syl6com mpcom 3impia
        ) FNOZBHOZGEOZCGPZEOZCEEQROZUQURSZUSVATZABCDEFHIJKLUAVCVBCMUBZPZEOZMEUC
        ZVDVCEUDOZVISZVBVHTUQVJURUQVIVIEFLUEZVKUFUGMCEEUHUIVGVAMGEVEGUJVFUTEVEG
        CUKULUMUNUOUP $.
        $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.  $d O x $.
      tpgprop1.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by an element is
         open . $)
      tpgprop1 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
        -> ( O .t { A } ) e. J ) $=
        ( ctopgrp wcel w3a csn co cima cgr cpw wceq topgrpgrp 3ad2ant1 simp2
        wi crn cuni wss topgrpbs eqtr ex pwuni pweq sseq2d mpbiri syl6 ax-mp
        syl sseld a1d 3imp prsubrtr2 syl111anc trhom2 eqeltrd ) FOPZBHPZGEPZQZG
        BRISZCGTZEVKDUAPZVIGHUBZPZVLVMUCVHVIVNVJDFLUDUEVHVIVJUFVHVIVJVPVHVJVPUG
        VIVHEVOGVHDUHZEUIZUCZEVOUJZDEFLMUKHVQUCZVSVTUGJWAVSHVRUCZVTWAVSWBHVQVRU
        LUMWBVTEVRUBZUJEUNWBVOWCEHVRUOUPUQURUSUTVAVBVCABGCDIHKJNVDVEABCDEFGHJKL
        MVFVG $.
        $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $}

    ${
      $d A y $.  $d G x y $.  $d J y $.  $d K x y $.  $d O x y $.  $d X x y $.
      tpgprop2.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by another set is
         open . $)
      tpgprop2 $p |- ( ( K e. TopGrp /\ A e. ~P X /\ O e. J )
        -> ( O .t A ) e. J ) $=
        ( vy ctopgrp wcel cpw w3a co cv csn ciun cgr cdm wceq topgrpgrp
        3ad2ant1 wa crn grprndm syl5req syl adantr pweq eleq2d cuni wss
        topgrpbs wi eqtr sseq2d ctop topgrptop eqid topge syl5bir mpan mpcom
        sseld imp 3adant2 syl5eq biimpa 3adant3 iscst4 syl111anc wral simpl1
        elelpwi expcom 3ad2ant2 simpl3 cmpt tpgprop1 r19.21aiva iunopn
        syl11anc eqeltrd ) FPQZBHRZQZGEQZSZGBITZOBGOUAZUBITZUCZEWNDUDQZGDUEUEZR
        ZQZBXAQZWOWRUFWJWLWSWMDFLUGZUHWJWMXBWLWTHUFZWJWMUIZXBWJXEWMWJWSXEXDWSDU
        JZWTHDUKZJULUMUNXEXBGWKQZXFXEXAWKGWTHUOUPWJWMXIWJEWKGXGEUQZUFZWJEWKURZD
        EFLMUSHXGUFZXKWJXLUTZJXMXKUIHXJUFZXNHXGXJVAXOXLEXJRZURZWJXOWKXPEHXJUOVB
        WJEVCQZXQEFMVDZEXJXJVEVFUMVGUMVHVIVJVKVGVIVLWJWLXCWMWJWLXCWJWKXABWJHWTU
        FWKXAUFWJXGWTHWJWSXGWTUFXDXHUMJVMHWTUOUMUPVNVOOGBDIUDWTWTVENVPVQWNXRWQE
        QZOBVRWREQWJWLXRWMXSUHWNXTOBWNWPBQZUIWJWPHQZWMXTWJWLWMYAVSWNYAYBWLWJYAY
        BUTWMYAWLYBWPBHVTWAWBVKWJWLWMYAWCAWPAHAUAWPDTWDZDEFGHIJYCVELMNWEVQWFOBW
        QEWGWHWI $.
        $( [1-Jan-2011] $)
    $}

  $}

  ${
    $d A x y $.  $d G x y $.  $d J x y $.  $d X x y $.
    ltrhom.1 $e |- X = ran G $.
    ltrhom.2 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrhom.3 $e |- G = ( 1st ` K ) $.
    ltrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a left translation is a homeomorphism. ` A ` is
       constant. $)
    ltrhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
      ( vy ctopgrp wcel wa chomeosm co cuni wf1o ccn ccnv w3a wceq crn
      topgrpbs syl5eq adantr wb f1oeq23 anidms eqcoms cgr ltrooo topgrpgrp
      sylan syl5bir mpcom cv cop cmpt ccom copab ltrcmp df-mpt eqcomi a1i
      coeq2d eqtrd ctop ctx topgrptop eqid txtop syl11anc wi eqtr ttcn2 eleq2
      anbi2d id eqidd mpteq12dv eleq1d imbi12d mpbiri syl mpan topgrpcn cnco
      syl32anc eqeltrd cgn cfv cgs ltrinvlem grpinvcl syl5req eleqtrrd anbi1d
      opabbidv mpbird 3jca cvv ltrset hmeobc syl111anc ) FMNZBGNZOZCEEPQNZERZXK
      CSZCEETQZNZCUAZXMNZUBZXIXLXNXPGXKUCZXIXLXGXRXHXGDUDZXKGDEFJKUEZHUFUGZXRXL
      GGCSZXIXLYBUHZXKGXKGUCZYCXKGXKGCUIUJUKDULNZXHYBXGABCDGIHUMDFJUNZUOUPUQXIC
      DAGBAURZUSZUTZVAZXMYEXHCYJUCXGYEXHOZCDYGGNZLURZYHUCOALVBZVAYJALBCDGIHVCYK
      YNYIDYNYIUCYKYIYNALGYHVDVEVFVGVHYFUOXIEVINZEEVJQZVINZYOYIEYPTQZNZDYPETQNZ
      YJXMNXGYOXHEFKVKZUGZXGYQXHXGYOYOYQUUAUUAEEYPYPVLVMVNUGZUUBXSXKUCZXIYSXGUU
      DXHXTUGGXSUCZUUDXIYSVOZHUUEUUDOXRUUFGXSXKVPXRUUFXGBXKNZOZAXKYHUTZYRNZVOYO
      UUGUUJXGALBUUIEXKXKVLZALXKYHVDVQUUAUOXRXIUUHYSUUJXRXHUUGXGGXKBVRVSXRYIUUI
      YRXRAGYHXKYHXRVTXRYHWAWBWCWDWEWFWGUQXGYTXHDEFJKWHUGZYIDEYPEWIWJWKXIXOAGBD
      WLWMZWMZYGDQUTZXMYEXHXOUUOUCXGABDWNWMZCDUUMGIHUUPVLUUMVLZWOYFUOXIUUODYLYM
      UUNYGUSUCZOZALVBZVAZXMXIYEUUNGNZUUOUVAUCXGYEXHYFUGZYEXHUVBXGBDUUMGHUUQWPY
      FUOZALUUNUUODGUUOVLHVCVNXIYOYQYOUUTYRNZYTUVAXMNUUBUUCUUBXIUVEYGXKNZUUROZA
      LVBZYRNZXIYOUUNXKNUVIUUBXIUUNGXKUVDXGYDXHXGXSXKGXTHWQUGWRALUUNUVHEXKUUKUV
      HVLVQVNXIXRUVEUVIUHYAXRUUTUVHYRXRUUSUVGALXRYLUVFUURGXKYGVRWSWTWCWFXAUULUU
      TDEYPEWIWJWKWKXBXIYOYOCXCNZXJXQUHUUBUUBXIYEUVJUVCABCDGIHXDWFXCCEEXKXKUUKU
      UKXEXFXA $.
      $( [12-Jul-2012] $)
  $}


  ${
    $d A x y $.  $d B x y $.  $d G x y $.  $d J y $.  $d K x $.  $d X x y $.
    lrtrhom.1 $e |- X = ran G $.
    lrtrhom.2 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    lrtrhom.3 $e |- G = ( 1st ` K ) $.
    lrtrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a right and left translation is a
       homeomorphism.  Note that ` A ` and ` B ` are constant. $)
    lrtrhom $p |- ( ( K e. TopGrp /\ A e. X /\ B e. X )
       -> F e. ( J Homeo J ) ) $=
      ( vy ctopgrp wcel w3a cv co cmpt ccom chomeosm cgr wceq eqid cmperltr
      topgrpgrp syl3an1 ctop wa topgrptop 3jca 3ad2ant1 trhom 3adant2 ltrhom
      3adant3 jca cmphmp sylc eqeltrd ) GNOZBHOZCHOZPZDMHBMQERSZAHAQCERSZTZFFUA
      RZEUBOVBVCDVGUCVAAMBCVEDEVFHJVEUDZVFUDZIUEEGKUFUGVDFUHOZVKVKPZVFVHOZVEVHO
      ZUIVGVHOVAVBVLVCVAVKVKVKFGLUJZVOVOUKULVDVMVNVAVCVMVBACVFEFGHIVJKLUMUNVAVB
      VNVCMBVEEFGHIVIKLUOUPUQVFVEFFFURUSUT $.
      $( [12-Jul-2012] $)
  $}

  ${
    $d A o u v x $.  $d B o u v x $.  $d G a b o u v $.  $d X a b o u v x $.
    grphmlem0.1 $e |- H = { o | E. a e. X E. b e. X
         o = ( x e. X |-> ( ( a G x ) G b ) ) } $.
    $( Condition for a left and right translation to belong to the class of all
       the left and right translations over the class ` X ` . $)
    grphmlem0 $p |- ( ( A e. X /\ B e. X /\ X e. C )
       -> ( x e. X |-> ( ( A G x ) G B ) ) e. H ) $=
      ( vu vv wcel w3a cv co cmpt wceq wrex cab eqid opreq1 opreq1d adantr
      mpteq2dv eqeq2d opreq2 rcla42ev mp3an3 3adant3 cvv wb mptexg 3ad2ant3
      eqeq1 rexbidva elabg syl mpbird weq wa simpl rexbidv opreq2d cbvrexv
      syl6bb abbii syl6eleqr ) BHNZCHNZHDNZOZAHBAPZFQZCFQZRZEPZAHIPZVNFQZJPZFQZ
      RZSZJHTZIHTZEUAZGVMVQVRAHLPZVNFQZMPZFQZRZSZMHTZLHTZEUAZWGVMVQWPNZVQWLSZMH
      TZLHTZVJVKWTVLVJVKVQVQSZWTVQUBWRXAVQAHVOWJFQZRZSLMBCHHWHBSZWLXCVQXDAHWKXB
      XDWKXBSVNHNZXDWIVOWJFWHBVNFUCUDUEUFUGWJCSZXCVQVQXFAHXBVPXFXBVPSXEWJCVOFUH
      UEUFUGUIUJUKVMVQULNZWQWTUMVLVJXGVKAHVPDUNUOWOWTEVQULVRVQSZWNWSLHXHWNWSUMW
      HHNXHWMWRMHXHWMWRUMWJHNVRVQWLUPUEUQUEUQURUSUTWFWOEWEWNILHILVAZWEVRAHWIWAF
      QZRZSZJHTWNXIWDXLJHXIWCXKVRXIAHWBXJXIXEVBZVTWIWAFXMVSWHVNFXIXEVCUDUDUFUGV
      DXLWMJMHJMVAZXKWLVRXNAHXJWKXNXEVBWAWJWIFXNXEVCVEUFUGVFVGVFVHVIKVI $.
      $( [16-Jul-2012] $)
  $}

  ${
    grphm.1 $e |- X = ran G $.
    grphm.2 $e |- G = ( 1st ` K ) $.
    grphm.3 $e |- J = ( 2nd ` K ) $.
    grphm.4 $e |- H = { o | E. a e. X E. b e. X
         o = ( p e. X |-> ( ( a G p ) G b ) ) } $.

    ${
      $d G a b c d e f o p $.  $d G c d e f p q $.  $d G c d p q z $.
      $d H c d e f y $.  $d K c d e f x y $.  $d X a b c d e f o p $.
      $d X c d e f p q $.  $d o x y $.
      $( Lemma for ~ grphm .  Closure. $)
      grphmlem1 $p |- ( K e. TopGrp
       -> A. x e. H
             A. y e. H ( x o. y ) e. H ) $=
        ( vc vd ve vf vq vz ctopgrp wcel cv ccom wa co cmpt wceq wrex cab wi
        eleq2i anbi12i weq opreq1 opreq1d adantr mpteq2dv eqeq2d opreq2
        cbvrex2v abbii simpl opreq2d bitri ax-17 cbvmpt eqeq2i simpr coeq12d
        eleq1d imbi2d w3a cgr topgrpgrp 3ad2ant3 anim12i ancoms 3adant3 eqid
        cmprltr syl111anc simp2l simp1l grpcl simp1r simp2r eqidd rcla42ev cvv
        crn c1st cfv fvex eqeltri rnex mptexg ax-mp a1i eqeq1d 2rexbidva elab3
        sylibr syl6eleqr eqeltrd 3exp syl5bir ex sylbi com4l com12 r19.23aivv
        com3l imp visset elabf wb eqeq1 syl2anb r19.21aivv ) GUBUCZAUDZBUDZUEZE
        UCZABEEYCEUCZYDEUCZUFZYBYFYIYCCUDZIHPUDZIUDZDUGZQUDZDUGZUHZUIZQHUJPHUJZ
        CUKZUCZYDYJIHRUDZYLDUGZSUDZDUGZUHZUIZSHUJRHUJZCUKZUCZUFZYBYFULZYIYCYJIH
        JUDZYLDUGZKUDZDUGZUHZUIZKHUJJHUJZCUKZUCZYDUUSUCZUFUUJYGUUTYHUVAEUUSYCOU
        MEUUSYDOUMUNUUTYTUVAUUIUUSYSYCUURYRCUUQYQYJIHYMUUNDUGZUHZUIJKPQHHJPUOZU
        UPUVCYJUVDIHUUOUVBUVDUUOUVBUIYLHUCZUVDUUMYMUUNDUULYKYLDUPUQURUSUTKQUOZU
        VCYPYJUVFIHUVBYOUVFUVBYOUIUVEUUNYNYMDVAURUSUTVBVCUMUUSUUHYDUURUUGCUUQUU
        FYJIHUUBUUNDUGZUHZUIJKRSHHJRUOZUUPUVHYJUVIIHUUOUVGUVIUVEUFUUMUUBUUNDUVI
        UUMUUBUIUVEUULUUAYLDUPURUQUSUTKSUOZUVHUUEYJUVJIHUVGUUDUVJUVEUFUUNUUCUUB
        DUVJUVEVDVEUSUTVBVCUMUNVFYCYPUIZQHUJPHUJZYDUUEUIZSHUJRHUJZUUKYTUUIUVLUV
        NUUKUVKUVNUUKULPQHHUVNYKHUCZYNHUCZUFZUVKUUKUVMUVQUVKUUKULULZRSHHUVMUUAH
        UCZUUCHUCZUFZUVRUVKUVMUWAUVQUUKUVKYCTHYKTUDZDUGZYNDUGZUHZUIZUVMUWAUVQUU
        KULZULZULYPUWEYCITUAHYOUWDUAUDZYOUCTVGUWIUWDUCIVGITUOYMUWCYNDYLUWBYKDVA
        UQVHVIUWFUVMUWHUWFUVMUFZUWGUVQYBUWEUUEUEZEUCZULZULUWAUWJUUKUWMUVQUWJYFU
        WLYBUWJYEUWKEUWJYCUWEYDUUEUWFUVMVDUWFUVMVJVKVLVMVMUWAUVQYBUWLUWAUVQYBVN
        ZUWKIHYKUUADUGZYLDUGZUUCYNDUGZDUGZUHZEUWNDVOUCZUVOUVSUFZUVPUVTUFZUWKUWS
        UIYBUWAUWTUVQDGMVPVQZUWAUVQUXAYBUVQUWAUXAUVQUVOUWAUVSUVOUVPVDUVSUVTVDVR
        VSVTUWAUVQUXBYBUVQUWAUXBUVQUVPUWAUVTUVOUVPVJUVSUVTVJVRVSVTITYKUUAYNUUCU
        UEUWEDHUWEWAUUEWALWBWCUWNUWSUUSEUWNUWSUUPUIZKHUJJHUJZUWSUUSUCUWNUWOHUCZ
        UWQHUCZUWSUWSUIZUXEUWNUWTUVOUVSUXFUXCUWAUVOUVPYBWDUVSUVTUVQYBWEYKUUADHL
        WFWCUWNUWTUVTUVPUXGUXCUVSUVTUVQYBWGUWAUVOUVPYBWHUUCYNDHLWFWCUWNUWSWIUXD
        UXHUWSIHUWPUUNDUGZUHZUIJKUWOUWQHHUULUWOUIZUUPUXJUWSUXKIHUUOUXIUXKUUOUXI
        UIUVEUXKUUMUWPUUNDUULUWOYLDUPUQURUSUTUUNUWQUIZUXJUWSUWSUXLIHUXIUWRUXLUV
        EUFUUNUWQUWPDUXLUVEVDVEUSUTWJWCUURUXECUWSUWSWKUCZUXEHWKUCUXMHDWLWKLDDGW
        MWNWKMGWMWOWPWQWPIHUWRWKWRWSWTYJUWSUIZUUQUXDJKHHUXNUULHUCUUNHUCUFZUFYJU
        WSUUPUXNUXOVDXAXBXCXDOXEXFXGXHXIXJXKXLXMXNXMXOYRUVLCYCUVLCVGAXPCAUOZYQU
        VKPQHHUXPUVQUFYJYCYPUXPUVQVDXAXBXQUUGUVNCYDUVNCVGBXPCBUOZUUFUVMRSHHUXQU
        UFUVMXRUWAYJYDUUEXSURXBXQXTXJXLYA $.
        $( [17-Jul-2012] $) $( [26-Jun-2012] $)
    $}

    ${
      $d G a b c d e i p o $.  $d H g h x y z $.  $d K a b c d e f g h i p $.
      $d X a b c d e i p o $.  $d f g h o x y z $.
      $( Lemma for ~ grphm .  Associativity . $)
      grphmlem2 $p |- ( K e. TopGrp
                      -> A. f
                         e. H
                            A. g
                            e. H
                               A. h
                               e. H
                                    ( ( f
                                        {
                                        <.
                                        <. x
                                        , y
                                        >.
                                        , z
                                        >.
                                        | ( x e. H
                                           /\ y e. H
                                           /\ z = ( x o. y ) ) }
                                        g )
                                      {
                                      <.
                                      <. x
                                      , y
                                      >.
                                      , z
                                      >.
                                      | ( x e. H /\ y e. H /\ z = ( x o. y ) )
                                      }
                                      h )
                                  = ( f
                                      {
                                      <.
                                      <. x
                                      , y
                                      >.
                                      , z
                                      >.
                                      | ( x e. H /\ y e. H /\ z = ( x o. y ) )
                                      }
                                      ( g
                                        {
                                        <.
                                        <. x
                                        , y
                                        >.
                                        , z
                                        >.
                                        | ( x e. H
                                           /\ y e. H
                                           /\ z = ( x o. y ) ) }
                                        h ) ) ) $=
        ( vc vd ve vi ctopgrp wcel cv ccom wceq w3a copab2 co wral wa wi cmpt
        wrex cab visset weq simpl eqeq1d 2rexbidva elab coeq2 coeq2d eqeq12d
        coeq1 coeq1d cgr topgrpgrp adantl anim12i adantr simpr 3jca eqid
        cmprltr2 syl ad2antlr simplll simplrl grpcl syl111anc simplrr simpllr
        simprr syl112anc simprl syl122anc grpass syl11anc opreq1d jca eqcomd
        opreq12d mpteq2dv 3eqtr4rd 3eqtrd syl5bir imp expdimp exp3a exp4c ex
        com14 3imp impcom com12 r19.23aivv 3exp com3l opreq1 eqeq2d opreq2
        cbvrex2v syl2anb eqeq1 rexbidv eleq2i sylbi eleq2s r19.21aiva
        r19.21aivva ax-17 cvv grphmlem1 ra42 coex a1i df-3an oprabbii
        oprabval2g exp3acom3r com23 imp31 ralbida 2ralbida mpbird coexg opreqi
        syl5eq opreq2d ) KUDUEZDUFZEUFZAUFZIUEZBUFZIUEZCUFUUFUUHUGZUHZUIZABCUJZ
        UKZFUFZUUMUKZUUDUUEUUOUUMUKZUUMUKZUHZFIULZEIULDIULUUDUUEUGZUUOUUMUKZUUD
        UUEUUOUGZUUMUKZUHZFIULZEIULDIULZUUCUVGUVAUUOUGZUUDUVCUGZUHZFIULZEIULDIU
        LUUCUVKDEIIUUCUUDIUEZUUEIUEZUMZUMZUVJFIUUOIUEZUVOUVJUVOUVJUNZUUOGUFZMLN
        UFZMUFZHUKZOUFZHUKZUOZUHZOLUPZNLUPZGUQZIUUOUWHUEUUOUWDUHZOLUPNLUPZUVQUW
        GUWJGUUOFURZGFUSZUWEUWINOLLUWLUVSLUEZUWBLUEZUMZUMUVRUUOUWDUWLUWOUTVAVBV
        CUVOUWJUVJUVNUUCUWJUVJUNZUUDUWHUEZUUEUWHUEZUUCUWPUNZUVLUVMUUDUWDUHZOLUP
        ZNLUPZUUEUWDUHZOLUPZNLUPZUWSUWQUWRUUDMLTUFZUVTHUKZUAUFZHUKZUOZUHZUALUPT
        LUPZUUEMLUBUFZUVTHUKZUCUFZHUKZUOZUHZUCLUPUBLUPZUWSUXBUXEUXLUXSUWSUXKUXS
        UWSUNTUALLUXSUXFLUEZUXHLUEZUMZUXKUWSUXRUYBUXKUWSUNZUNUBUCLLUXMLUEZUXOLU
        EZUMZUXRUYBUYCUYFUXRUYBUIZUXKUUCUWPUWJUYGUXKUUCUIZUVJUWIUYHUVJUNZNOLLUW
        IUWOUYIUWIUWOUYHUVJUWIUVJUVAUWDUGZUUDUUEUWDUGZUGZUHZUWOUYHUMUWIUVHUYJUV
        IUYLUUOUWDUVAVDUWIUVCUYKUUDUUOUWDUUEVDVEVFUYHUWOUYMUYGUXKUUCUWOUYMUNZUY
        FUXRUYBUXKUUCUYNUNZUNUXKUXRUYBUYFUYOUXKUXRUYBUYFUYOUNUNUXKUXRUMZUYBUYFU
        UCUYNUYPUYBUYFUMZUUCUMZUWOUYMUXKUXRUYRUWOUMZUYMUXKUYMUXJUUEUGZUWDUGZUXJ
        UYKUGZUHZUXRUYSUMUXKUYJVUAUYLVUBUXKUVAUYTUWDUUDUXJUUEVGVHUUDUXJUYKVGVFU
        XRUYSVUCUXRVUCUXJUXQUGZUWDUGZUXJUXQUWDUGZUGZUHUYSUXRVUAVUEVUBVUGUXRUYTV
        UDUWDUUEUXQUXJVDVHUXRUYKVUFUXJUUEUXQUWDVGVEVFUYSVUEMLUXFUXMHUKZUVTHUKUX
        OUXHHUKZHUKUOZUWDUGZMLVUHUVSHUKZUVTHUKZUWBVUIHUKZHUKZUOZVUGUYSVUDVUJUWD
        UYSHVIUEZUXTUYDUMZUYAUYEUMZUIZVUDVUJUHUYRVUTUWOUYRVUQVURVUSUUCVUQUYQHKQ
        VJZVKZUYQVURUUCUYBUXTUYFUYDUXTUYAUTUYDUYEUTVLVMUYQVUSUUCUYBUYAUYFUYEUXT
        UYAVNUYDUYEVNVLVMVOVMMMUXFUXMUXHUXOUXQUXJHLUXJVPZUXQVPZPVQVRVHUYSVUQVUH
        LUEZUWMUMVUILUEZUWNVUKVUPUHUUCVUQUYQUWOVVAVSZUYRVVEUWOUWMUYRVUQUXTUYDVV
        EVVBUXTUYAUYFUUCVTZUYBUYDUYEUUCWAZUXFUXMHLPWBWCUWMUWNUTVLUYSVUQUYEUYAVV
        FVVGUYRUYEUWOUYBUYDUYEUUCWDVMZUYRUYAUWOUXTUYAUYFUUCWEVMZUXOUXHHLPWBWCUY
        RUWMUWNWFZMMVUHUVSVUIUWBUWDVUJHLVUJVPUWDVPZPVQWGUYSUXJMLUXMUVSHUKZUVTHU
        KUWBUXOHUKZHUKUOZUGZMLUXFVVNHUKZUVTHUKZVVOUXHHUKZHUKZUOZVUGVUPUYSVUQUXT
        VVNLUEZUYAVVOLUEZVVQVWBUHVVGUYRUXTUWOVVHVMZUYSVUQUYDUWMVWCVVGUYRUYDUWOV
        VIVMZUYRUWMUWNWHZUXMUVSHLPWBWCVVKUYSVUQUWNUYEVWDVVGVVLVVJUWBUXOHLPWBWCM
        MUXFVVNUXHVVOVVPUXJHLVVCVVPVPPVQWIUYSVUFVVPUXJUYSVUQUYDUWMUYEUWNVUFVVPU
        HVVGVWFVWGVVJVVLMMUXMUVSUXOUWBUWDUXQHLVVDVVMPVQWIVEUYSMLVUOVWAUYSUVTLUE
        ZUMZVUMVVSVUNVVTHVWIVULVVRUVTHVWIVUQUXTUYDUWMUIZVULVVRUHUYSVUQVWHVVGVMU
        YSVWJVWHUYSUXTUYDUWMVWEVWFVWGVOVMUXFUXMUVSHLPWJWKWLVWIVUQUWNUYEUYAUIZUM
        ZVUNVVTUHUYSVWLVWHUYSVUQVWKVVGUYSUWNUYEUYAVVLVVJVVKVOWMVMVWLVVTVUNUWBUX
        OUXHHLPWJWNVRWOWPWQWRWSWTWSXAXBXCXDXEXFXFXGWSXBXHXIXHXJXJXIXKXIWTUWTUXK
        UUDMLUXGUWBHUKZUOZUHNOTUALLNTUSZUWDVWNUUDVWOMLUWCVWMVWOUWCVWMUHVWHVWOUW
        AUXGUWBHUVSUXFUVTHXLWLVMWPXMOUAUSZVWNUXJUUDVWPMLVWMUXIVWPVWMUXIUHVWHUWB
        UXHUXGHXNVMWPXMXOUXCUXRUUEMLUXNUWBHUKZUOZUHNOUBUCLLNUBUSZUWDVWRUUEVWSML
        UWCVWQVWSUWCVWQUHVWHVWSUWAUXNUWBHUVSUXMUVTHXLWLVMWPXMOUCUSZVWRUXQUUEVWT
        MLVWQUXPVWTVWQUXPUHVWHUWBUXOUXNHXNVMWPXMXOXPUWGUXBGUUDDURZGDUSZUWFUXANL
        VXBUWEUWTOLUVRUUDUWDXQXRXRVCUWGUXEGUUEEURZGEUSZUWFUXDNLVXDUWEUXCOLUVRUU
        EUWDXQXRXRVCXPIUWHUUDSXSIUWHUUESXSXPXGXHXTSYAXGYBYCUUCUVFUVKDEIIUUCDYDZ
        UUCEYDZUVOUVEUVJFIUVOFYDZUVOUVPUMZUVBUVHUVDUVIVXHUVAIUEZUVPUVHYEUEZUVBU
        VHUHUVOVXIUVPUUCUVNVXIUUCVXIEIULDIULUVNVXIUNDEGHIJKLMNOPQRSYFVXIDEIIYGV
        RWTVMUVOUVPVNZVXJVXHUVAUUOUUDUUEVXAVXCYHUWKYHYIABCUVAUUOIIUUJUVHUUMUVAU
        UHUGYEUUFUVAUUHVGUUHUUOUVAVDUULUUGUUIUMUUKUMZABCUUGUUIUUKYJYKZYLWCVXHUV
        LUVCIUEZUVIYEUEZUVDUVIUHUUCUVLUVMUVPWAZUUCUVNUVPVXNUVNUUCUVPVXNUNZUVMUU
        CVXQUNUVLUVMUVPUUCVXNUUCUVMUVPVXNUUCVXNFIULEIULUVMUVPUMVXNUNEFGHIJKLMNO
        PQRSYFVXNEFIIYGVRYMYNVKXHYOVXOVXHUUDUVCVXAUUEUUOVXCUWKYHZYHYIABCUUDUVCI
        IUUJUVIUUMUUDUUHUGZYEUUFUUDUUHVGZUUHUVCUUDVDVXMYLWCVFYPYQYRUUCUUTUVFDEI
        IVXEVXFUVOUUSUVEFIVXGVXHUUPUVBUURUVDVXHUUNUVAUUOUUMVXHUVLUVMUVAYEUEZUUN
        UVAUHVXPUUCUVLUVMUVPWDZUVNVYAUUCUVPUUDUUEIIYSVSABCUUDUUEIIUUJUVAUUMVXSY
        EVXTUUHUUEUUDVDVXMYLWCWLVXHUUQUVCUUDUUMVXHUVMUVPUVCYEUEZUUQUVCUHVYBVXKV
        YCVXHVXRYIUVMUVPVYCUIUUEUUOVXLABCUJZUKUVCUUQABCUUEUUOIIUUJUVCVYDUUEUUHU
        GYEUUFUUEUUHVGUUHUUOUUEVDVYDVPYLUUMVYDUUEUUOVXMYTUUAWCUUBVFYPYQYR $.
        $( [17-Jul-2012] $) $( [29-Jun-2012] $)
    $}

    ${
      $d G a b f o p u v x y z $.  $d H f x y z $.  $d K a b f p $.
      $d X a b f o p u x y z $.

      $( Lemma for ~ grphm .  Neutral element . $)
      grphlem3 $p |- ( ( K e. TopGrp
                         /\ ( x
                               e. X
                               |-> ( ( ( Id ` G ) G x ) G ( Id ` G ) ) )
                            e. H )
                      -> A. f
                         e. H
                              ( ( x
                                e. X
                                |-> ( ( ( Id ` G ) G x ) G ( Id ` G ) ) )
                                {
                                <.
                                <. x
                                , y
                                >.
                                , z
                                >.
                                | ( x e. H /\ y e. H /\ z = ( x o. y ) ) }
                                f )
                            = f ) $=
        ( vu vv ctopgrp wcel cgi cfv cv co cmpt ccom wceq w3a copab2 wral wa
        wi cvv wrex cab cgr topgrpgrp eqid grpidcl syl adantr ax-17 weq opreq2
        opreq1d cbvmpt a1i simpl mpteq2dv eqeq2d opreq2d rcla42ev syl111anc
        crn c1st fvex eqeltri rnex mptexg ax-mp eqeq1 rexbidv elab sylibr
        syl6eleqr simpr visset coex coeq1 coeq2 df-3an oprabbii oprabval2g id
        eqeq12d imbi2d adantl simpll simplr cmprltr2 syl122anc eqidd grplid ex
        impcom adantlr grprid com12 imp opreq12d mpteq12dv eqtrd syl5cbir
        r19.23aivv sylbi eleq2s opreq1i syl5eq r19.21aiv eleq1i sylan2b ) ITUAZ
        RJFUBUCZRUDZFUEZYDFUEZUFZGUAZAJYDAUDZFUEZYDFUEZUFZDUDZYJGUAZBUDZGUAZCUD
        YJYPUGZUHZUIZABCUJZUEZYNUHZDGUKYMGUAYCYIULUUCDGYCYNGUAZUUCUMYIYCUUDUUCY
        CUUDULZYHYNUUAUEZYNUUBUUEUUFYHYNUGZYNUUEYIUUDUUGUNUAZUUFUUGUHUUEYHEUDZK
        JLUDZKUDZFUEZMUDZFUEZUFZUHZMJUOZLJUOZEUPZGUUEYHUUOUHZMJUOZLJUOZYHUUSUAU
        UEYDJUAZUVCYHKJYDUUKFUEZYDFUEZUFZUHZUVBYCUVCUUDYCFUQUAZUVCFIOURZYDFJNYD
        USZUTVAZVBZUVLUVGUUERKSJYGUVESUDZYGUAZKVCUVMUVEUARVCRKVDYFUVDYDFYEUUKYD
        FVEVFVGVHUUTUVGYHKJUVDUUMFUEZUFZUHLMYDYDJJUUJYDUHZUUOUVPYHUVQKJUUNUVOUV
        QUUKJUAZULZUULUVDUUMFUVSUUJYDUUKFUVQUVRVIVFVFVJVKUUMYDUHZUVPUVFYHUVTKJU
        VOUVEUVTUVRULUUMYDUVDFUVTUVRVIVLVJVKVMVNUURUVBEYHJUNUAYHUNUAJFVOUNNFFIV
        PUCUNOIVPVQVRVSVRRJYGUNVTWAZUUIYHUHZUUQUVALJUWBUUPUUTMJUUIYHUUOWBWCWCWD
        WEQWFYCUUDWGUUHUUEYHYNUWADWHZWIVHABCYHYNGGYRUUGUUAYHYPUGUNYJYHYPWJYPYNY
        HWKYTYOYQULYSULABCYOYQYSWLWMWNVNUUDYCUUGYNUHZYCUWDUMZYNUUSGYNUUSUAYNUUO
        UHZMJUOZLJUOZUWEUURUWHEYNUWCEDVDZUUQUWGLJUWIUUPUWFMJUUIYNUUOWBWCWCWDUWF
        UWELMJJUWFUWEYCYHUUOUGZUUOUHZUMUUJJUAZUUMJUAZULZUWFUWDUWKYCUWFUUGUWJYNU
        UOYNUUOYHWKUWFWOWPWQUWNYCUWKUWNYCULZUWJKJYDUUJFUEZUUKFUEZUUMYDFUEZFUEZU
        FZUUOUWOUVHUVCUWLUVCUWMUWJUWTUHYCUVHUWNUVIWRYCUVCUWNUVKWRZUWLUWMYCWSUXA
        UWLUWMYCWTKRYDUUJYDUUMUUOYHFJYHUSUUOUSNXAXBUWOKJUWSJUUNUWOJXCUWOUWQUULU
        WRUUMFUWLYCUWQUULUHUWMUWLYCULUWPUUJUUKFYCUWLUWPUUJUHZYCUVHUWLUXBUMUVIUV
        HUWLUXBUUJYDFJNUVJXDXEVAXFVFXGUWNYCUWRUUMUHZUWMYCUXCUMUWLYCUWMUXCYCUVHU
        WMUXCUMUVIUVHUWMUXCUUMYDFJNUVJXHXEVAXIWRXJXKXLXMXEXNXOXPQXQXFXMYMYHYNUU
        AARSJYLYGUVMYLUARVCUVNAVCARVDYKYFYDFYJYEYDFVEVFVGZXRXSXEVBXTYMYHGUXDYAY
        B $.
        $( [17-Jul-2012] $) $( [2-Jul-2012] $)
    $}

    ${
      $d G o p $.  $d H h $.  $d J a b h o $.  $d K a b h $.  $d K a b p $.
      $d X b o p $.
      $( Lemma for ~ grphm .  Homeomorphisms . $)
      grphmlem4 $p |- ( K e. TopGrp -> H C_ ( J Homeo J ) ) $=
        ( vh ctopgrp wcel chomeosm co cv cuni wf1o cima wral ccnv w3a wi cmpt
        wceq wrex cab visset weq eqeq1 rexbidv elab wa f1oeq1 imaeq1 eleq1d
        ralbidv cnveq imaeq1d 3anbi123d imbi2d crn topgrpbs eqtr wb f1oeq23
        anidms cgr topgrpgrp eqid rltrooo 3exp syl syl5bir eqcoms mpan mpcom
        com3l 3imp ctop topgrptop jca 3ad2ant3 lrtrhom 3coml hmeocnb sylc
        hmeocna 3jca 3expia syl5cbir r19.23aivv sylbi eleq2s com12 cvv a1i
        ishomeo syl111anc sylibrd ssrdv ) EOPZNCDDQRZXENSZCPZDTZXIXGUAZXGASZUBZ
        DPZADUCZXGUDZXKUBZDPZADUCZUEZXGXFPZXHXEXSXEXSUFZXGXKGFHSZGSBRISZBRUGZUH
        ZIFUIZHFUIZAUJZCXGYHPXGYDUHZIFUIZHFUIZYAYGYKAXGNUKZANULZYFYJHFYMYEYIIFX
        KXGYDUMUNUNUOYIYAHIFFYIYAXEXIXIYDUAZYDXKUBZDPZADUCZYDUDZXKUBZDPZADUCZUE
        ZUFYBFPZYCFPZUPYIXSUUBXEYIXJYNXNYQXRUUAXIXIXGYDUQYIXMYPADYIXLYODXGYDXKU
        RUSUTYIXQYTADYIXPYSDYIXOYRXKXGYDVAVBUSUTVCVDUUCUUDXEUUBUUCUUDXEUEZYNYQU
        UAUUCUUDXEYNXEUUCUUDYNBVEZXIUHZXEUUCUUDYNUFZUFZBDEKLVFFUUFUHZUUGXEUUIUF
        ZJUUJUUGUPFXIUHUUKFUUFXIVGUUKXIFXIFUHZUUIUUCUUDFFYDUAZUFZUFZXEUULUUHUUN
        UUCUULYNUUMUUDUULYNUUMVHXIFXIFYDVIVJVDVDXEBVKPZUUOBEKVLUUPUUCUUDUUMGYBY
        CYDBFYDVMZJVNVOVPVQVRVPVSVTWAWBUUEDWCPZUURUPZYDXFPZYQXEUUCUUSUUDXEUURUU
        RDELWDZUVAWEWFXEUUCUUDUUTGYBYCYDBDEFJUUQKLWGWHZAYDDDWIWJUUEUUSUUTUUAUUE
        UURUURXEUUCUURUUDUVAWFZUVCWEUVBAYDDDWKWJWLWMWNWOWPMWQWRXEUURUURXGWSPZXT
        XSVHUVAUVAUVDXEYLWTAWSXGDDXIXIXIVMZUVEXAXBXCXD $.
        $( [12-Jul-2012] $)
    $}

    ${
      $d G a b g p u x y z $.  $d G a b o p u $.  $d G b g p u v x y z $.
      $d G p u v w x $.  $d H g u v x y z $.  $d K f u v $.  $d K u v x $.
      $d X a b g p u x y z $.  $d X a b o p u $.  $d X b g p u v x y z $.
      $d a b f g u $.  $d f o u v $.
      $( Lemma for ~ grphm .  Inverse element . $)
      grphlem5 $p |- ( K e. TopGrp
                -> A. f
                   e. H E. g e. H
                        ( g
                          {
                          <.
                          <. x
                          , y
                          >.
                          , z
                          >.
                          | ( x e. H /\ y e. H /\ z = ( x o. y ) ) }
                          f )
                      = ( x
                         e. X
                         |-> ( ( ( Id ` G ) G x ) G ( Id ` G ) ) ) ) $=
        ( vu vv vw ctopgrp wcel cv ccom wceq w3a copab2 co cgi cfv cmpt wrex
        wi cab visset weq wb wa simpl eqeq1d rexbidva adantr elab opreq1d
        mpteq2dv eqeq2d opreq2d cbvrex2v cgn cvv cgr topgrpgrp adantl simpll
        eqid grpinvcl syl11anc simplr crn rnexg syl syl5eqel grphmlem0
        syl111anc opreq2 rexbidv imbi2d oprabval2gc cmprltr2 syl122anc grplinv
        ad2antlr simpllr grprinv opreq12d ax-17 cbvmpt syl5eq 3eqtrd opreq1
        rcla4ev expcom com12 syl5bir com3l mpcom ex com23 r19.23aivv sylbi
        eleq2s r19.21aiv ) JUBUCZEUDZDUDZAUDZHUCBUDZHUCCUDXQXRUEUFUGABCUHZUIZAK
        GUJUKZXQGUIZYAGUIZULZUFZEHUMZDHXPHUCXNYFXNYFUNZXPFUDZLKMUDZLUDZGUIZNUDZ
        GUIZULZUFZNKUMZMKUMZFUOZHXPYRUCXPYNUFZNKUMZMKUMZYGYQUUAFXPDUPFDUQZYPYTM
        KUUBYPYTURYIKUCUUBYOYSNKUUBYLKUCZUSYHXPYNUUBUUCUTVAVBVCVBVDUUAXPLKSUDZY
        JGUIZTUDZGUIZULZUFZTKUMSKUMYGYSUUIXPLKUUEYLGUIZULZUFMNSTKKMSUQZYNUUKXPU
        ULLKYMUUJUULYJKUCZUSZYKUUEYLGUUNYIUUDYJGUULUUMUTVEVEVFVGNTUQZUUKUUHXPUU
        OLKUUJUUGUUOUUMUSYLUUFUUEGUUOUUMUTVHVFVGVIUUIYGSTKKUUDKUCZUUFKUCZUSZXNU
        UIYFUURXNUUIYFUNZLKUUDGVJUKZUKZYJGUIUUFUUTUKZGUIULZHUCZUURXNUSZUUSUVEUV
        AKUCZUVBKUCZKVKUCZUVDUVEGVLUCZUUPUVFXNUVIUURGJPVMZVNZUUPUUQXNVOZUUDGUUT
        KOUUTVPZVQVRZUVEUVIUUQUVGUVKUUPUUQXNVSZUUFGUUTKOUVMVQVRZUVEGVTZVKKUVEUV
        IUVQVKUCUVKGVLWAWBOWCZLUVAUVBVKFGHKMNRWDWEZUUIUVDUVEYFUUIUVEYFUNUVEXOUU
        HXSUIZYDUFZEHUMZUNUVDUUIYFUWBUVEUUIYEUWAEHUUIXTUVTYDXPUUHXOXSWFVAWGWHUV
        EUVDUWBUVEUVCUUHXSUIZYDUFZUVDUWBUNUVEUWCUVCUUHUEZLKUVAUUDGUIZYJGUIZUUFU
        VBGUIZGUIZULZYDUVEUVDUUHHUCZUWCUWEUFUVSUVEUUPUUQUVHUWKUVLUVOUVRLUUDUUFV
        KFGHKMNRWDWEABCUVCUUHHHXSXSVPWIVRUVEUVIUVFUUPUVGUUQUWEUWJUFUVKUVNUVLUVP
        UVOLLUVAUUDUVBUUFUUHUVCGKUVCVPUUHVPOWJWKUVEAKUWFXQGUIZUWHGUIZULYDUWJUVE
        AKUWMYCUVEXQKUCZUSZUWLYBUWHYAGUWOUWFYAXQGUVEUWFYAUFZUWNUVEUVIUUPUWPUVKU
        VLUUDYAGUUTKOYAVPZUVMWLVRVCVEUWOUVIUUQUWHYAUFXNUVIUURUWNUVJWMUUPUUQXNUW
        NWNUUFYAGUUTKOUWQUVMWOVRWPVFLAUAKUWIUWMUAUDZUWIUCAWQUWRUWMUCLWQLAUQUWGU
        WLUWHGYJXQUWFGWFVEWRWSWTUVDUWDUWBUWAUWDEUVCHXOUVCUFUVTUWCYDXOUVCUUHXSXA
        VAXBXCWBXDXEXFXGXHXIXJXKXKRXLXDXM $.
        $( [16-Jul-2012] $)
    $}

    ${
      $d G a a0 a1 b b0 b1 o p x y $.  $d G a b f g o p u x y z $.
      $d G a0 a1 b0 b1 p q $.  $d G a b o p u v $.  $d G p w x $.
      $d H a b f g h o u x y z $.  $d J a b o $.
      $d K a a0 a1 b b0 b1 o p x y $.  $d K a b f g h o p u x y $.
      $d X a a0 a1 b b0 b1 o p x y $.  $d X a b f g o p u x y z $.
      $d X a0 a1 b0 b1 p q $.  $d X a b o p u v $.  $d a1 b b1 o p x y z $.
      $d q z $.
      $( The functions of equation ` ( ( a G p ) G b ) ` form a group of
         homeomorphisms. $)
      grphm $p |- ( K e. TopGrp -> ( { <. <. x , y >. , z >. |
         ( x e. H /\ y e. H /\ z = ( x o. y ) ) } e. Grp /\
         H C_ ( J Homeo J ) ) ) $=
        ( vf vg vh vu vv bnjva1 bnjvb1 bnjva0 bnjvb0 vq vw ctopgrp wcel cv
        ccom wceq w3a copab2 cgr chomeosm co wss crn cxp wf wral wrex wa cmpt
        cab wex wb wal wi eqid weq opreq1 opreq1d adantr mpteq2dv eqeq2d
        opreq2 rcla42ev mp3an3 cvv topgrpgrp unsgrp syl adantl mptexg eqeq1
        rexbidv elabg mpbird cbvrexv syl6bb abbii syl6eleqr 3adant2 c1st cfv
        cgi grpidcl simpl fveq2 eqcomd eleq1d syl5bir ax-mp simpr grpcl
        syl111anc jca eqcomi eleq1i sylanb fveq2i grprid grplid eqtr2d a1d
        eqidd mpteq12dv 3exp com3r syl6 com4t pm2.43i rneqi eqtr4i eleq2s
        mpcom impcom rnexg syl5eqel 3ad2ant3 simp2 simp1 3adant2r simp2r 3jca
        r19.21aiv fopab2a sylib crimmt1 eqtr4d ax-17 coeq2 rcla4e syl11anc
        coeq1 r2ex df-3an bicomi 2exbii bitri r19.23aivv com12 visset a1i
        opreq12d anbi12d ex cbvexd bnj1158 eleq2i simpll simplr 3bitri coeq12d
        simp2ll simp2rl simp2rr simp2lr anim12i cbvmpt coeq1d cmprltr eqtrd
        biimpd 3imp opreq2d syl6bi com4r com23 com3l sylbi 19.23advv impbid
        19.21aiv eq2ab sylibr 3anbi3d 2exbidv cbvabv syl6eq rnoprab 3eqtr4g
        xpeq12 anidms feq23 mpancom id raleqdv raleqbidv rexeqdv anbi2d
        rexeqbidv 3anbi123d eqcoms grphmlem1 oprabbii foprab2 grphmlem2 fvex
        eqeltri rnex elab r19.26 c2nd grphlem3 grphlem5 sylanbrc eqeq1d eqeq2
        ralbidv rcla4edv mpd mpdan ab2rexex pm3.2i oprabex2g isgrp grphmlem4 )
        HUHUIZAUJZFUIZBUJZFUIZCUJZUYLUYNUKZULZUMZABCUNZUOUIZFGGUPUQURUYKVUAUYTU
        SZVUBUTZVUBUYTVAZQUJZRUJZUYTUQSUJZUYTUQVUEVUFVUGUYTUQUYTUQULZSVUBVBZRVU
        BVBZQVUBVBZTUJZVUEUYTUQZVUEULZVUFVUEUYTUQZVULULZRVUBVCZVDZQVUBVBZTVUBVC
        ZUMZFVUBULZUYKVVAUYKDUJZJIKUJZJUJZEUQZLUJZEUQZVEZULZLIVCZKIVCZDVFZUYSBV
        GAVGZCVFZFVUBUYKVVMUYMUYOVVCUYQULZUMZBVGAVGZDVFZVVOUYKVVLVVRVHZDVIVVMVV
        SULUYKVVTDUYKVVLVVRVVLUYKVVRVVJUYKVVRVJKLIIVVDIUIZVVGIUIZVDZVVJUYKVVRVW
        CVVJUYKUMZVVPBFVCZAFVCZVVRVWDVVIFUIZVVCVVIUYNUKZULZBFVCZVWFVWCUYKVWGVVJ
        VWCUYKVDZVVIVVMFVWKVVIVVCJIVULVVEEUQZUAUJZEUQZVEZULZUAIVCZTIVCZDVFZVVMV
        WKVVIVWSUIZVVIVWOULZUAIVCZTIVCZVWCVXCUYKVWAVWBVVIVVIULZVXCVVIVKZVXAVXDV
        VIJIVVFVWMEUQZVEZULTUAVVDVVGIITKVLZVWOVXGVVIVXHJIVWNVXFVXHVWNVXFULVVEIU
        IZVXHVWLVVFVWMEVULVVDVVEEVMVNVOVPVQUALVLZVXGVVIVVIVXJJIVXFVVHVXJVXFVVHU
        LVXIVWMVVGVVFEVRVOVPVQVSVTVOVWKVVIWAUIZVWTVXCVHVWKIWAUIZVXKUYKVXLVWCUYK
        EUOUIZVXLEHNWBZEIMWCWDWEJIVVHWAWFWDVWRVXCDVVIWAVVJVWQVXBTIVVJVWPVXAUAIV
        VCVVIVWOWGWHWHWIWDWJVVLVWRDVVKVWQKTIKTVLZVVKVVCJIVWLVVGEUQZVEZULZLIVCVW
        QVXOVVJVXRLIVXOVVIVXQVVCVXOJIVVHVXPVXOVVHVXPULVXIVXOVVFVWLVVGEVVDVULVVE
        EVMVNVOVPVQWHVXRVWPLUAILUAVLZVXQVWOVVCVXSJIVXPVWNVXSVXPVWNULVXIVVGVWMVW
        LEVRVOVPVQWKWLWKWMWNPWNWOVWDJIVVEVEZFUIVVCVVIVXTUKZULZVWJVWDVXTVVMFVWDV
        XTVVMUIZVXTVVIULZLIVCZKIVCZVWCUYKVYFVVJUYKVWCVYFUYKHWPWQZUOUIZVWCVYFVJZ
        VYGHVYGVKWBVYGWRWQZVYGUSZUIVYHVYIVYJVYGVYKVYKVKVYJVKWSVYHVYIVJZVYJIVYKV
        YJIUIZVYLVYHVWCVYMVYMVYFVYHVWCVXTJIVYJVVEEUQZVYJEUQZVEZULZVYMVYMVYFVJVJ
        VYHVYQVWCVYHJIVVEVYOVYHVXIVDZVYOVYNVVEVYRVXMVYNIUIZVDZVYOVYNULVXMVXIVYT
        VYHVXMVXIVDZVXMVYSVXMVXIWTZWUAVXMVYMVXIVYSWUBEVYGULZWUAVYMVJNWUCVYMEWRW
        QZIUIZWUAWUCVYJWUDIWUCWUDVYJEVYGWRXAXBXCVXMWUEVXIWUDEIMWUDVKWSZVOXDXEVX
        MVXIXFVYJVVEEIMXGXHXIVYGEUOEVYGNXJZXKZXLVYNVYJEIMVYGEWRWUGXMZXNWDVXMVXI
        VYNVVEULVYHVVEVYJEIMWUIXOWUHXLXPVPXQVYMVYMVYQVYFVYMVYMVYQVYFVYDVYQVXTJI
        VYNVVGEUQZVEZULKLVYJVYJIIVVDVYJULZVVIWUKVXTWULJIVVHIWUJWULIXRWULVVFVYNV
        VGEVVDVYJVVEEVMVNXSVQVVGVYJULZWUKVYPVXTWUMJIWUJIVYOWUMIXRVVGVYJVYNEVRXS
        VQVSXTYAYBYCYDVYKEUSZIVYGEWUGYEMYFYGYHWDYIWOVWDVXTWAUIZVYCVYFVHUYKVWCWU
        OVVJUYKVXLWUOUYKWUNWAIUYKVXMWUNWAUIVXNEUOYJWDMYKJIVVEWAWFWDYLVVLVYFDVXT
        WAVVCVXTULZVVKVYEKIWUPVVJVYDLIVVCVXTVVIWGWHWHWIWDWJPWNVWDVVCVVIVYAVWCVV
        JUYKYMVWDIIVVIVAZVYAVVIULVWCUYKWUQVVJVWKVVHIUIZJIVBWUQVWKWURJIVWKVXIVXM
        VVFIUIZVWBUMZWURUYKVWCVXIWUTVJZUYKVXMVWCWVAVJVXNVXMVWCVXIWUTVXMVWCVXIUM
        VXMWUSVWBVXMVWCVXIYNVXMVWAVXIWUSVWBVVDVVEEIMXGYOVXMVWAVWBVXIYPYQXTWDYIV
        VFVVGEIMXGYBYRJIIVVHVVIVXEYSYTWOJIIVVIUUAWDUUBVWIVYBBVXTFVYBBUUCUYNVXTU
        LVWHVYAVVCUYNVXTVVIUUDVQUUEUUFVWEVWJAVVIFVWJAUUCUYLVVIULZVVPVWIBFWVBUYQ
        VWHVVCUYLVVIUYNUUGVQWHUUEUUFVWFUYMUYOVDZVVPVDZBVGAVGVVRVVPABFFUUHWVDVVQ
        ABVVQWVDUYMUYOVVPUUIUUJUUKUULYTXTUUMUUNUYKVVQVVLABVVQUYKVVLUYMUYOVVPUYK
        VVLVJZUYOVVPWVEVJZVJZUYLVVMFUYLVVMUIZWVBLIVCZKIVCZWVGUYLWAUIWVHWVJVHAUU
        OVVLWVJDUYLWADAVLZVVKWVIKIWVKVVJWVBLIVVCUYLVVIWGWHWHWIXEWVJUYLJIUBUJZVV
        EEUQZUCUJZEUQZVEZULZUCIVCZUBIVCZWVGWVIWVRKUBIKUBVLZWVBWVQLUCIIWVTVWBWVB
        VDZWVNIUIZWVQVDZLUCWVTUCUUCWWAWWAUCVIVJWVTWWAUCUUCUUPWVTLUCVLZWWAWWCVHW
        VTWWDVDZVWBWWBWVBWVQWWEVVGWVNIWVTWWDXFZXCWWEVVIWVPUYLWWEJIVVHWVOWWEVVHW
        VOULVXIWWEVVFWVMVVGWVNEWWEVVDWVLVVEEWVTWWDWTVNWWFUUQVOVPVQUURUUSUUTUVAW
        KUYOWVSWVFUYOUYNJIUDUJZVVEEUQZUEUJZEUQZVEZULZUEIVCZUDIVCZWVSWVFVJZUYOUY
        NVVMUIZUYNVVIULZLIVCZKIVCZWWNFVVMUYNPUVBUYNWAUIWWPWWSVHBUUOVVLWWSDUYNWA
        DBVLZVVKWWRKIWWTVVJWWQLIVVCUYNVVIWGWHWHWIXEWWRWWMKUDIKUDVLZWWQWWLLUEIIW
        XAVWBWWQVDZWWIIUIZWWLVDZLUEWXAUEUUCWXBWXBUEVIVJWXAWXBUEUUCUUPWXALUEVLZW
        XBWXDVHWXAWXEVDZVWBWXCWWQWWLWXFVVGWWIIWXAWXEXFXCWXFVVIWWKUYNWXFJIVVHWWJ
        WXFVXIVDZVVFWWHVVGWWIEWXGVVDWWGVVEEWXAWXEVXIUVCVNWXAWXEVXIUVDUUQVPVQUUR
        UUSUUTUVAWKUVEWWLWWOUDUEIIWVSWWGIUIZWXCVDZWWLWVFWVQWXIWWLWVFVJZVJUBUCII
        WVLIUIZWWBVDZWXIWVQWXJWXLWXIWVQWXJVJWVQWWLVVPWXLWXIVDZWVEWVQWWLVVPWXMWV
        EVJZVJWVQWWLVDZVVPVVCWVPWWKUKZULZWXNWXOUYQWXPVVCWXOUYLWVPUYNWWKWVQWWLWT
        WVQWWLXFUVFVQWXQWXMUYKVVLWXQWXMUYKUMZWVLWWGEUQZIUIZWWIWVNEUQZIUIZVVCJIW
        XSVVEEUQZWYAEUQZVEZULZVVLWXRVXMWXKWXHWXTUYKWXQVXMWXMVXNYLZWXKWWBWXIWXQU
        YKUVGWXHWXCWXLWXQUYKUVHWVLWWGEIMXGXHWXRVXMWXCWWBWYBWYGWXHWXCWXLWXQUYKUV
        IWXKWWBWXIWXQUYKUVJWWIWVNEIMXGXHWXQWXMUYKWYFWXMUYKWXQWYFWXMUYKWXQWYFVJW
        XMUYKVDZWXQWYFWYHWXPWYEVVCWYHVXMWXKWXHVDZWWBWXCVDZWXPWYEULUYKVXMWXMVXNW
        EWXMWYIUYKWXLWXKWXIWXHWXKWWBWTWXHWXCWTUVKVOWXMWYJUYKWXLWWBWXIWXCWXKWWBX
        FWXHWXCXFUVKVOVXMWYIWYJUMZWXPUFIWVLUFUJZEUQZWVNEUQZVEZWWKUKWYEWYKWVPWYO
        WWKWVPWYOULWYKJUFCIWVOWYNUYPWVOUIUFUUCUYPWYNUIJUUCJUFVLWVMWYMWVNEVVEWYL
        WVLEVRVNUVLUUPUVMJUFWVLWWGWVNWWIWWKWYOEIWYOVKWWKVKMUVNUVOXHVQUVPUUSYAUV
        QVVJWYFVVCJIWYCVVGEUQZVEZULKLWXSWYAIIVVDWXSULZVVIWYQVVCWYRJIVVHWYPWYRVV
        HWYPULVXIWYRVVFWYCVVGEVVDWXSVVEEVMVNVOVPVQVVGWYAULZWYQWYEVVCWYSJIWYPWYD
        WYSVXIVDVVGWYAWYCEWYSVXIWTUVRVPVQVSXHXTUVSUUSUVTUUSUWAUUMUWBUUMUWCUUNUW
        CUWCPYGUVQUUNUWDUWEUWFVVLVVRDUWGUWHVVRVVNDCDCVLZVVQUYSABWYTVVPUYRUYMUYO
        VVCUYPUYQWGUWIUWJUWKUWLPUYSABCUWMUWNVVBVVAFFUTZFUYTVAZVUHSFVBZRFVBZQFVB
        ZVUNVUPRFVCZVDZQFVBZTFVCZUMZUYKVVAXUJVHVUBFVUBFULZVUDXUBVUKXUEVUTXUIVUC
        XUAULZXUKVUDXUBVHXUKXULVUBFVUBFUWOUWPVUCVUBXUAFUYTUWQUWRXUKVUJXUDQVUBFX
        UKUWSZXUKVUIXUCRVUBFXUMXUKVUHSVUBFXUMUWTUXAUXAXUKVUSXUHTVUBFXUMXUKVURXU
        GQVUBFXUMXUKVUQXUFVUNXUKVUPRVUBFXUMUXBUXCUXAUXDUXEUXFUYKXUBXUEXUIUYKUYQ
        FUIBFVBAFVBXUBABDEFGHIJKLMNOPUXGABCFFUYQFUYTUYSWVCUYRVDABCUYMUYOUYRUUIU
        XHZUXIYTABCQRSDEFGHIJKLMNOPUXJUYKAIWUDUYLEUQZWUDEUQZVEZFUIZXUIUYKXUQVVM
        FUYKXUQVVIULZLIVCZKIVCZXUQVVMUIUYKWUEWUEXUQJIWUDVVEEUQZWUDEUQZVEZULZXVA
        UYKVXMWUEVXNWUFWDZXVFXVEUYKAJUGIXUPXVCUGUJZXUPUIJUUCXVGXVCUIAUUCAJVLXUO
        XVBWUDEUYLVVEWUDEVRVNUVLUUPXUSXVEXUQJIXVBVVGEUQZVEZULKLWUDWUDIIVVDWUDUL
        ZVVIXVIXUQXVJJIVVHIXVHXVJIXRXVJVVFXVBVVGEVVDWUDVVEEVMVNXSVQVVGWUDULZXVI
        XVDXUQXVKJIXVHIXVCXVKIXRVVGWUDXVBEVRXSVQVSXHVVLXVADXUQVXLXUQWAUIIWUNWAM
        EEVYGWANHWPUXKUXLUXMUXLZAIXUPWAWFXEVVCXUQULZVVKXUTKIXVMVVJXUSLIVVCXUQVV
        IWGWHWHUXNUWHPWNUYKXURVDZXUQVUEUYTUQZVUEULZVUOXUQULZRFVCZVDZQFVBZXUIXVT
        XVPQFVBXVRQFVBZXVNXVPXVRQFUXOABCQDEFHUXPWQZHIJKLMNXWBVKPUXQUYKXWAXURABC
        QRDEFGHIJKLMNOPUXRVOUXSUYKXUHXVTTXUQFVULXUQULZXUHXVTVHUYKXWCXUGXVSQFXWC
        VUNXVPXUFXVRXWCVUMXVOVUEVULXUQVUEUYTVMUXTXWCVUPXVQRFVULXUQVUOUYAWHUURUY
        BWEUYCUYDUYEYQXDYHUYKUYTWAUIZVUAVVAVHUYKFWAUIZXWEVDZXWDXWFUYKXWEXWEFVVM
        WAPKLDIIVVIXVLXVLUYFUXLZXWGUYGUUPABCFFUYQWAWAUYTXUNUYHWDQRSTWAUYTVUBVUB
        VKUYIWDWJDEFGHIJKLMNOPUYJXI $.
        $( [17-Jul-2012] $)
    $}
  $}

  $c opfn $.

  $( Extend class notation with an operator that derives an operation on
     functions from an operation on the elements of the common range of those
     functions. $)
  copfn $a class opfn $.


  ${
    $d g i h x y z a b $.
    $( Multiplication or addition of two functions ` x ` and ` y ` derived from
       the operation ` g ` on the elements of the common range of ` x ` and
       ` y ` .  The functions ` x ` and ` y ` must also have the same domain
       ` i ` . $)
    df-opfn $a |- opfn = { <. <. g , i >. , h >. |
       h = { <. <. x , y >. , z >. |
         ( x e. ( dom dom g ^m i ) /\ y e. ( dom dom g ^m i ) /\
       z = { <. a , b >. | ( a e. dom x /\ b = ( ( x ` a ) g ( y ` a ) ) ) } )
        }
        } $.
  $}

