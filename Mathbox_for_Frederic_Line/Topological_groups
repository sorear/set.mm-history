$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topological groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TopGrp $.

  $( Extend class notation with the class of all topological groups. $)
  ctopgrp $a class TopGrp $.

  ${
    $d g j $.
    $( Define the class of all topological groups.  A topological group is a
       group whose operation and inverse function are continuous. $)
    df-topgrp $a |- TopGrp = { <. g , j >. | ( ( g e. GrpOp /\ j e. Top ) /\
         g e. ( ( j tX j ) Cn j ) /\ ( inv ` g ) e. ( j Cn j ) ) } $.
  $}

  ${
    $d G g j $.  $d J g j $.  $d x y $.
    $( The predicate "is a topological group".  Bourbaki TG III.1 (Contributed
       by FL, 18-Apr-2010.) $)
    istopgrp $p |- ( J e. A -> ( <. G , J >. e. TopGrp <->
      ( ( G e. GrpOp /\ J e. Top ) /\ G e. ( ( J tX J ) Cn J )
        /\ ( inv ` G ) e. ( J Cn J ) ) ) ) $=
      ( vx vy vg vj wcel cop ctopgrp cgr ctop wa ctx co ccn cgn cfv w3a cvv
      wbr df-br wrel cv copab relopab df-topgrp releqi mpbir brrelexi sylbir
      adantl simpl jca elisset adantr 3ad2ant1 eleq2i wceq eleq1 anbi1d fveq2
      eleq1d 3anbi123d anbi2d id opreq12d eleq2d opelopabg syl5bb pm5.21nd ) CA
      HZBCIZJHZBKHZCLHZMZBCCNOZCPOZHZBQRZCCPOZHZSZBTHZVLMZVLVNMWEVLVNWEVLVNBCJU
      AWEBCJUBBCJJUCDUDZKHEUDZLHMWGWHWHNOWHPOHWGQRWHWHPOHSZDEUEZUCWIDEUFJWJDEUG
      UHUIUJUKULVLVNUMUNVLWDMWEVLWDWEVLVQVTWEWCVOWEVPBKUOUPUQULVLWDUMUNVNVMFUDZ
      KHZGUDZLHZMZWKWMWMNOZWMPOZHZWKQRZWMWMPOZHZSZFGUEZHWFWDJXCVMFGUGURXBVOWNMZ
      BWQHZWAWTHZSWDFGBCTAWKBUSZWOXDWRXEXAXFXGWLVOWNWKBKUTVAWKBWQUTXGWSWAWTWKBQ
      VBVCVDWMCUSZXDVQXEVTXFWCXHWNVPVOWMCLUTVEXHWQVSBXHWPVRWMCPXHWMCWMCNXHVFZXI
      VGXIVGVHXHWTWBWAXHWMCWMCPXIXIVGVHVDVIVJVK $.
      $( [18-Apr-2010] $)
  $}

  ${
    topgrpi.1 $e |- G = ( 1st ` K ) $.
    ${
      topgrpi.2 $e |- J = ( 2nd ` K ) $.
      ${
        $d G g j $.  $d J g j $.  $d K g j $.  $d N g j $.
        topgrpi.3 $e |- N = ( inv ` G ) $.
        $( "Axioms" of a topological group.  (Contributed by FL,
           18-Apr-2010.) $)
        topgrpi $p |- ( K e. TopGrp -> ( ( G e. GrpOp /\ J e. Top ) /\
          G e. ( ( J tX J ) Cn J ) /\ N e. ( J Cn J ) ) ) $=
          ( vg vj ctopgrp wcel cv cgr ctop wa ctx co ccn cgn cfv w3a copab
          df-topgrp eleq2i c1st wceq wb eqtr3 eqcomd eleq1 anbi1d fveq2
          syl6eqr eleq1d 3anbi123d syl mpan c2nd anbi2d opreq12 anidms mpancom
          eleq2d elopabi sylbi ) CJKCHLZMKZILZNKZOZVFVHVHPQZVHRQZKZVFSTZVHVHRQZ
          KZUAZHIUBZKAMKZBNKZOZABBPQZBRQZKZDBBRQZKZUAZJVRCHIUCUDVQVSVIOZAVLKZDV
          OKZUAZWGHICACUETZUFZVFWLUFZVQWKUGZEWMWNOZVFAUFZWOWPAVFAVFWLUHUIWQVJWH
          VMWIVPWJWQVGVSVIVFAMUJUKVFAVLUJWQVNDVOWQVNASTDVFASULGUMUNUOUPUQBCURTZ
          UFZVHWRUFZWKWGUGZFWSWTOZVHBUFZXAXBBVHBVHWRUHUIXCWHWAWIWDWJWFXCVIVTVSV
          HBNUJUSXCVLWCAVKWBUFZXCVLWCUFXCXDVHBVHBPUTVAVKWBVHBRUTVBVCXCVOWEDXCVO
          WEUFVHBVHBRUTVAVCUOUPUQVDVE $.
          $( [18-Apr-2010] $)

        $( In a topological group, the inverse function is continuous.
           (Contributed by FL, 21-Jun-2010.) $)
        topgrpinv $p |- ( K e. TopGrp -> N e. ( J Cn J ) ) $=
          ( ctopgrp wcel cgr ctop wa ctx co ccn topgrpi simp3d ) CHIAJIBKILABBM
          NBONIDBBONIABCDEFGPQ $.
          $( [21-Jun-2010] $)
      $}

      $( In a topological group, the base set of the topology and the base set
         of the group are equal.  (Contributed by FL, 21-Jun-2010.) $)
      topgrpbs $p |- ( K e. TopGrp -> ran G = U. J ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv w3a crn cuni wceq eqid
        topgrpi wf1o wi grpinvf wf cnf f1of cdm fdm eqtr2 ex syl2im syl
        syl5com 3exp pm2.43i com3r imp31 3adant2 ) CFGAHGZBIGZJZABBKLBMLGZANOZB
        BMLGZPAQZBRZSZABCVDDEVDTZUAVBVEVHVCUTVAVEVHUTVFVFVDUBZVAVEVHUCUCAVDVFVF
        TVIUDVAVEVJVHVAVEVJVHUCZUCVAVAVEVKVAVAVEPVGVGVDUEZVJVHVDBBVGVGVGTZVMUFV
        JVFVFVDUEZVLVHUCVFVFVDUGVNVDUHZVFSZVLVOVGSZVHVFVFVDUIVGVGVDUIVPVQVHVOVF
        VGUJUKULUMUNUOUPUQUMURUSUM $.
        $( [21-Jun-2010] $)

      $( In a topological group, the operation ` G ` is continuous.
         (Contributed by FL, 21-Jun-2010.) $)
      topgrpcn $p |- ( K e. TopGrp -> G e. ( ( J tX J ) Cn J ) ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv eqid topgrpi simp2d ) CFG
        AHGBIGJABBKLBMLGANOZBBMLGABCSDESPQR $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the operation ` G ` is a group.  (Contributed by
       FL, 21-Jun-2010.) $)
    topgrpgrp $p |- ( K e. TopGrp -> G e. GrpOp ) $=
      ( ctopgrp wcel cgr c2nd cfv ctop wa ctx co ccn cgn eqid topgrpi simp1d
      simpld ) BDEZAFEZBGHZIEZSTUBJAUAUAKLUAMLEANHZUAUAMLEAUABUCCUAOUCOPQR $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrptop.1 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, ` J ` is a topology.  (Contributed by FL,
       21-Jun-2010.) $)
    topgrptop $p |- ( K e. TopGrp -> J e. Top ) $=
      ( ctopgrp wcel c1st cfv cgr ctop wa ctx co ccn cgn eqid topgrpi simp1d
      simprd ) BDEZBFGZHEZAIEZSUAUBJTAAKLAMLETNGZAAMLETABUCTOCUCOPQR $.
      $( [21-Jun-2010] $)
  $}

  ${
    extopgrp.1 $e |- A e. B $.
    ${
      $d A x y $.
      $( The identity element of the trivial group.  (Contributed by FL,
         21-Jun-2010.) $)
      idtrgrp $p |- ( Id ` { <. <. A , A >. , A >. } ) = A $=
        ( vx vy cop csn cgi cfv cv co wceq wral crab cuni crn opex elisseti
        rnsnop eqcomi eqid grpsn grpidvallem wcel wa cab elsni adantr elsn
        biimpri copab fopabsn opreqi df-opr snid eqidd fvopab4 ax-mp 3eqtri
        opreq2 id 3eqtr4a syl rgen opreq1 eqeq1d ralbidv mpbiri jca impbii
        abbii df-rab df-sn 3eqtr4i unieqi unisn eqtri ) AAFZAFGZHIZDJZEJZVSKZWB
        LZEAGZMZDWENZOZAEDVTVSWEVSPWEVRAAAQZABCRZSTVTUAAWJUBUCWHWEOAWGWEWAWEUDZ
        WFUEZDUFWAALZDUFWGWEWLWMDWLWMWKWMWFWAAUGUHWMWKWFWKWMDAUIUJWMWFAWBVSKZWB
        LZEWEMWOEWEWBWEUDWBALZWOWBAUGWPAAVSKZAWNWBWQAAWAVRGZUDWPUEDEUKZKVRWSIZA
        VSWSAADEVRAWIWJULUMAAWSUNVRWRUDWTALVRWIUODEVRAAWRWSWAVRLAUPWSUAWJUQURUS
        WBAAVSUTWPVAVBVCVDWMWDWOEWEWMWCWNWBWAAWBVSVEVFVGVHVIVJVKWFDWEVLDAVMVNVO
        AWJVPVQVQ $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.
      $( The inverse function of the trivial group.  (Contributed by FL,
         21-Jun-2010.) $)
      invtrgrp $p |- ( inv ` { <. <. A , A >. , A >. } ) = ( _I |` { A } ) $=
        ( vx vy vz cop csn cgn cfv cv wcel co wceq crab cuni wa copab cid cres
        cgr elisseti grpsn crn opex rnsnop eqcomi cgi idtrgrp eqid grpoinvfval
        ax-mp cxp restidsing fconstopab elsn simpl opreq2d eqeq1d rabbidva
        unieqd df-opr fvsn eqtri wreu wb snid weu wal wex elsni adantr biimpri
        opreq1 syl6eq jca impbii ax-gen eqeq2 bibi2d albidv cla4egv mp2 df-eu
        mpbir df-reu reuuni2 bicomd mp2an syl6req sylbi eqeq2d pm5.32i opabbii
        3eqtrri ) AAGZAGHZIJZDKZAHZLZEKZFKZWSWQMZANZFWTOZPZNZQZDERZSWTTZWQUALWR
        XJNAABCUBZUCDFAEWQWRWTWQUDWTWPAAAUEZXLUFUGWQUHJAABCUIUGWRUJUKULXKWTWTUM
        XAXBANZQZDERXJAUNDEWTAUOXOXIDEXAXNXHXAAXGXBXAWSANZAXGNDAUPXPXGXCAWQMZAN
        ZFWTOZPZAXPXFXSXPXEXRFWTXPXCWTLZQZXDXQAYBWSAXCWQXPYAUQURUSUTVAXTANZAAWQ
        MZANZYDWPWQJAAAWQVBWPAXMXLVCVDZAWTLZXRFWTVEZYCYEVFAXLVGYHYAXRQZFVHZYJYI
        XCXBNZVFZFVIZEVJZABLYIXCANZVFZFVIZYNCYPFYIYOYAYOXRXCAVKVLYOYAXRYAYOFAUP
        VMYOXQYDAXCAAWQVNZYFVOVPVQVRYMYQEABXNYLYPFXNYKYOYIXBAXCVSVTWAWBWCYIFEWD
        WEXRFWTWFWEYGYHQYEYCXRYEFWTAYOXQYDAYRUSWGWHWIWEWJWKWLWMWNWOVD $.
        $( [21-Jun-2010] $)
    $}

    $( The trivial topological group.  (Contributed by FL, 21-Jun-2010.) $)
    extopgrp $p |- <. { <. <. A , A >. , A >. } , { (/) , { A } } >.
       e. TopGrp $=
      ( cop csn c0 cpr ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv cvv w3a wb
      prex istopgrp ax-mp elisseti grpsn indistop pm3.2i cxp wf eqid txtop
      mp2an wfo crn opex rnsnop eqcomi grpfo fof syl cuni wceq cun 0ex snex
      unipr uncom un0 3eqtrri txuni mapudiscn cid cres invtrgrp idcn eqeltri
      mpbir3an ) AADZADEZFAEZGZDHIZVSJIZWAKIZLZVSWAWAMNZWAONIZVSPQZWAWAONZIZWAR
      IWBWEWGWJSTFVTUARVSWAUBUCWCWDAABCUDZUEZVTUFZUGWFKIZVTVTUHZVTVSUIZWGWDWDWN
      WMWMWAWAWFWFUJZUKULWCWPWLWCWOVTVSUMWPVSVTVSUNVTVRAAAUOWKUPUQURWOVTVSUSUTU
      CWOVTVSWFWFVAZWOWDWDWRWOVBWMWMWAWAWFVTVTWQWAVAFVTVCVTFVCVTFVTVDAVEZVFFVTV
      GVTVHVIZWTVJULUQWSVKULWHVLVTVMZWIABCVNWDXAWIIWMWAVTWTVOUCVPVQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrpsubcn.1 $e |- G = ( 1st ` K ) $.
    topgrpsubcn.2 $e |- J = ( 2nd ` K ) $.
    topgrpsubcn.3 $e |- D = ( /g ` G ) $.
    ${
      $d G u x y z $.  $d J u x y $.
      topgrpsubcn.4 $e |- K e. TopGrp $.
      $( Lemma for ~ topgrpsubcn . $)
      topgrpsubcnlem $p |- D e. ( ( J tX J ) Cn J ) $=
        ( vx vy vz vu cv crn wcel wa cgn cfv co wceq copab2 ctx ccn ctopgrp
        cgr topgrpgrp eqid grpdivfval syl ax-mp cop ccom cxp wfn wfo grpfo
        fofn 3syl simpl wi grpoinvcl ex adantl oprab2co ctop w3a topgrptop
        txtop syl11anc 3jca cid cres fvresi adantr eqcomd opeq1d eqeq2d
        pm5.32i oprabbii cuni topgrpbs idcn topgrpinv 2txcn syl222anc syl5eqel
        topgrpcn jca32 cnco eqeltri ) AIMZBNZOZJMZWLOZPZKMWKWNBQRZRZBSTPIJKUAZC
        CUBSZCUCSZDUDOZAWSTZHXBBUEOZXCBDEUFZIJKABWQWLWLUGZWQUGZGUHUIUJWSBWPLMZW
        KWRUKZTZPZIJLUAZULZXAXBWSXMTZHXBBWLWLUMZUNZXNXBXDXOWLBUOXPXEBWLXFUPXOWL
        BUQURIJLKWLWLWKWRWLWLXLWSBWMWOUSWOWRWLOZWMXBWOXQUTZHXBXDXRXEXDWOXQWNBWQ
        WLXFXGVAVBUIUJVCXLUGWSUGVDUIUJWTVEOZXSCVEOZVFZXLWTWTUCSZOZBXAOZPPZXMXAO
        XBYEHXBYAYCYDXBXSXSXTXBXTXTXSCDFVGZYFCCWTWTUGZVHVIZYHYFVJXBXLWPXHWKVKWL
        VLZRZWRUKZTZPZIJLUAZYBXKYMIJLWPXJYLWPXIYKXHWPWKYJWRWPYJWKWMYJWKTWOWLWKV
        MVNVOVPVQVRVSXBXTXTXTXTYICCUCSZOZWQYOOYNYBOYFYFYFYFXBXTYPYFCWLXBWLCVTTH
        BCDEFWAUJZWBUIBCDWQEFXGWCIJLCCWTYIWQYNCCWTWLWLYGYGYQYQYNUGWDWEWFBCDEFWG
        WHUJXLBWTWTCWIUJWJWJ $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the "subtraction" (or "division") is
       continuous.  Bourbaki TG III.1 axiom GT' (Contributed by FL,
       21-Jun-2010.) $)
    topgrpsubcn $p |- ( K e. TopGrp -> D e. ( ( J tX J ) Cn J ) ) $=
      ( ctopgrp wcel c1st cfv cgs ctx co ccn fveq2i eqtri c2nd c0 cop csn cpr
      cif wceq fveq2 fveq2d opreq12d eleq12d eqid cvv 0ex extopgrp elimel
      topgrpsubcnlem dedth opreq12i syl6eleqr syl5eqel ) DHIZADJKZLKZCCMNZCONZA
      BLKVAGBUTLEPQUSVADRKZVDMNZVDONZVCUSVAVFIUSDSSTSTUASSUAUBTZUCZJKZLKZVHRKZV
      KMNZVKONZIDVGDVHUDZVAVJVFVMVNUTVILDVHJUEUFVNVEVLVDVKOVNVDVKVDVKMDVHRUEZVO
      UGVOUGUHVJVIVKVHVIUIVKUIVJUIDVGHSUJUKULUMUNUOVBVECVDOCVDCVDMFFUPFUPUQUR
      $.
      $( [21-Jun-2010] $)
  $}

  ${
    trhom.1 $e |- X = ran G $.
    trhom.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trhom.3 $e |- G = ( 1st ` K ) $.
    trhom.4 $e |- J = ( 2nd ` K ) $.
    ${
      $d A x y $.  $d G x y $.  $d K x y $.  $d X x y $.
      $( In a topological group a right translation is a homeomorphism.
         Bourbaki TG III.2 (Contributed by FL, 21-Jun-2010.) $)
      trhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
        ( vy ctopgrp wcel wa chomeosm co cuni wf1o ccn ccnv w3a crn wceq wi
        topgrpbs cgr topgrpgrp trooo ex syl wb eqtr f1oeq2 f1oeq3 bitrd eqcoms
        imbi2d mpan syl5ibr mpcom imp c2nd cfv cv cop copab ccom cxp wral wfn
        opelxpi ancoms r19.21aiva adantl wfo grpfo fofn 3syl adantr eqid cmpt
        df-mpt df-opr eqeq2i anbi2i opabbii 3eqtri fnopabco2b syl11anc ctop
        ctx topgrptop txtop 3jca eqcomi unieqi eqeq12i biimpi ttcn sylan eleq2
        anbi2d ax-17 anbi1d opabbid eleq1d imbi12d mpbiri anabsi5 topgrpcn
        cnco syl12anc eqeltrd opreq12 anidms eleq2d ax-mp cgs trinv grpdivfo
        eqtri topgrpsubcn cvv wf oprex a1i rgen fopab2a mpbi c1st fvex eqeltri
        rnex fex sylancr hmeobc syl111anc mpbird ) FMNZBGNZOZCEEPQNZERZUUDCSZCE
        ETQZNZCUAZUUFNZUBZUUBUUEUUGUUIYTUUAUUEDUCZUUDUDZYTUUAUUEUEZDEFJKUFZYTUU
        MUULUUAGGCSZUEZYTDUGNZUUPDFJUHZUUQUUAUUOABCDGIHUIUJUKGUUKUDZUULUUMUUPUL
        ZHUUSUULOGUUDUDZUUTGUUKUUDUMUVAUUEUUOUUAUUEUUOULUUDGUUDGUDUUEGUUDCSUUOU
        UDGUUDCUNUUDGGCUOUPUQURUKUSUTVAVBEFVCVDZUDZUUBUUGUEKUUBUUGUVCCUVBUVBTQZ
        NUUBCDAVEZGNZLVEZUVEBVFZUDZOZALVGZVHZUVDUUBUVHGGVIZNZAGVJZDUVMVKZCUVLUD
        UUAUVOYTUUAUVNAGUVFUUAUVNUVEBGGVLVMVNVOZYTUVPUUAYTUUQUVMGDVPUVPUURDGHVQ
        UVMGDVRVSVTALGUVHUVMUVKCDUVKWAZCAGUVEBDQZWBUVFUVGUVSUDZOZALVGUVFUVGUVHD
        VDZUDZOZALVGIALGUVSWCUWAUWDALUVTUWCUVFUVSUWBUVGUVEBDWDWEWFWGWHWIWJUUBUV
        BWKNZUVBUVBWLQZWKNZUWEUBZUVKUVBUWFTQZNZDUWFUVBTQZNZUVLUVDNYTUWHUUAYTUWE
        UWGUWEUVBFUVBWAZWMZYTUWEUWEUWGUWNUWNUVBUVBUWFUWFWAWNWJUWNWOVTZYTUUAUWJY
        TUULGUVBRZUDZUUBUWJUEZUUNUULUWQUUKGUUDUWPGUUKHWPEUVBKWQWRWSUWQUWRYTBUWP
        NZOZUVEUWPNZUVIOZALVGZUWINZUEYTUWEUWSUXDUWNALBUXCUVBUWPUWPWAUXCWAWTXAUW
        QUUBUWTUWJUXDUWQUUAUWSYTGUWPBXBXCUWQUVKUXCUWIUWQUVJUXBALUWQAXDUWQLXDUWQ
        UVFUXAUVIGUWPUVEXBXEXFXGXHXIVSXJZYTUWLUUADUVBFJUWMXKVTUVKDUVBUWFUVBXLXM
        XNUVCUUFUVDCUVCUUFUVDUDEUVBEUVBTXOXPZXQUTXRUUBUUHAGUVEBDXSVDZQZWBZUUFYT
        UUQUUAUUHUXIUDUURABUXGCDGIHUXGWAZXTXAUVCUUBUXIUUFNZUEKUUBUXKUVCUXIUVDNU
        UBUXIUXGUVKVHZUVDUUBUVOUXGUVMVKZUXIUXLUDUVQYTUXMUUAYTUUQUVMGUXGVPUXMUUR
        UXGDGHUXJYAUVMGUXGVRVSVTALGUVHUVMUVKUXIUXGUVRUXIUVFUVGUXHUDZOZALVGUVFUV
        GUVHUXGVDZUDZOZALVGALGUXHWCUXOUXRALUXNUXQUVFUXHUXPUVGUVEBUXGWDWEWFWGYBW
        IWJUUBUWHUWJUXGUWKNZUXLUVDNUWOUXEYTUXSUUAUXGDUVBFJUWMUXJYCVTUVKUXGUVBUW
        FUVBXLXMXNUVCUUFUVDUXIUXFXQUTXRXNWOUUBEWKNZUXTCYDNZUUCUUJULYTUXTUUAEFKW
        MVTZUYBUUBGYDCYEZGYDNZUYAUVSYDNZAGVJUYCUYEAGUYEUVFUVEBDYFYGYHAGYDUVSCIY
        IYJUYDUUBGUUKYDHDDFYKVDYDJFYKYLYMYNYMYGGYDYDCYOYPYDCEEUUDUUDUUDWAZUYFYQ
        YRYS $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.
      $( In a topological group, a converse of a right translation is a
         homeomorphism.  Bourbaki TG III.2 (Contributed by FL, 30-Dec-2010.) $)
      cnvtrhom $p |- ( ( K e. TopGrp /\ A e. X )
        -> `' F e. ( J Homeo J ) ) $=
        ( ctopgrp wcel wa ctop chomeosm co ccnv topgrptop jca adantr trhom
        cnvhmpha sylc ) FLMZBGMZNEOMZUGNZCEEPQZMCRUIMUEUHUFUEUGUGEFKSZUJTUAABCD
        EFGHIJKUBCEEUCUD $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d F y $.  $d G x $.  $d J y $.  $d K x $.  $d O y $.
      $d X x $.
      $( In a topological group the image of an open set by a right translation
         is an open set.  Bourbaki TG III.2 (Contributed by FL,
         30-Dec-2010.) $)
      trhom2 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
       -> ( F " O ) e. J ) $=
        ( vy ctopgrp wcel cima chomeosm co wa wi trhom cv wral ctop topgrptop
        jca adantr hmeocnb syl wceq imaeq2 eleq1d rcla4cv syl6com mpcom 3impia
        ) FNOZBHOZGEOZCGPZEOZCEEQROZUQURSZUSVATZABCDEFHIJKLUAVCVBCMUBZPZEOZMEUC
        ZVDVCEUDOZVISZVBVHTUQVJURUQVIVIEFLUEZVKUFUGMCEEUHUIVGVAMGEVEGUJVFUTEVEG
        CUKULUMUNUOUP $.
        $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.  $d O x $.
      tpgprop1.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by an element is
         open .  (Contributed by FL, 30-Dec-2010.) $)
      tpgprop1 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
        -> ( O .t { A } ) e. J ) $=
        ( ctopgrp wcel w3a csn co cima cgr cpw wceq topgrpgrp 3ad2ant1 simp2
        wi crn cuni wss topgrpbs eqtr ex pwuni pweq sseq2d mpbiri syl6 mpsyl
        sseld a1d 3imp prsubrtr2 syl111anc trhom2 eqeltrd ) FOPZBHPZGEPZQZGBRIS
        ZCGTZEVJDUAPZVHGHUBZPZVKVLUCVGVHVMVIDFLUDUEVGVHVIUFVGVHVIVOVGVIVOUGVHVG
        EVNGHDUHZUCZVGVPEUIZUCZEVNUJZJDEFLMUKVQVSHVRUCZVTVQVSWAHVPVRULUMWAVTEVR
        UBZUJEUNWAVNWBEHVRUOUPUQURUSUTVAVBABGCDIHKJNVCVDABCDEFGHJKLMVEVF $.
        $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $}

    ${
      $d A y $.  $d G x y $.  $d J y $.  $d K x y $.  $d O x y $.  $d X x y $.
      tpgprop2.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by another set is
         open .  (Contributed by FL, 1-Jan-2011.) $)
      tpgprop2 $p |- ( ( K e. TopGrp /\ A e. ~P X /\ O e. J )
        -> ( O .t A ) e. J ) $=
        ( vy ctopgrp wcel cpw w3a co cv csn ciun cgr cdm wceq topgrpgrp
        3ad2ant1 wa crn grprndm syl5req syl adantr cuni wss topgrpbs wi eqtr
        ctop topgrptop eqid topge pweq sseq2d syl5ibr mpan mpcom sseld imp
        eleq2d 3adant2 syl5eq biimpa 3adant3 iscst4 syl111anc wral simpl1
        elelpwi expcom 3ad2ant2 simpl3 cmpt tpgprop1 r19.21aiva iunopn
        syl11anc eqeltrd ) FPQZBHRZQZGEQZSZGBITZOBGOUAZUBITZUCZEWNDUDQZGDUEUEZR
        ZQZBXAQZWOWRUFWJWLWSWMDFLUGZUHWJWMXBWLWTHUFZWJWMUIZXBWJXEWMWJWSXEXDWSHD
        UJZWTJDUKZULUMUNXFXBXEGWKQZWJWMXIWJEWKGXGEUOZUFZWJEWKUPZDEFLMUQHXGUFZXK
        WJXLURZJXMXKUIHXJUFZXNHXGXJUSWJXLXOEXJRZUPZWJEUTQZXQEFMVAZEXJXJVBVCUMXO
        WKXPEHXJVDVEVFUMVGVHVIVJXEXAWKGWTHVDVKVFVHVLWJWLXCWMWJWLXCWJWKXABWJHWTU
        FWKXAUFWJHXGWTJWJWSXGWTUFXDXHUMVMHWTVDUMVKVNVOOGBDIUDWTWTVBNVPVQWNXRWQE
        QZOBVRWREQWJWLXRWMXSUHWNXTOBWNWPBQZUIWJWPHQZWMXTWJWLWMYAVSWNYAYBWLWJYAY
        BURWMYAWLYBWPBHVTWAWBVJWJWLWMYAWCAWPAHAUAWPDTWDZDEFGHIJYCVBLMNWEVQWFOBW
        QEWGWHWI $.
        $( [1-Jan-2011] $)
    $}

  $}

  ${
    $d A x y $.  $d G x y $.  $d J x y $.  $d X x y $.
    ltrhom.1 $e |- X = ran G $.
    ltrhom.2 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrhom.3 $e |- G = ( 1st ` K ) $.
    ltrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a left translation is a homeomorphism. ` A ` is
       constant.  (Contributed by FL, 12-Jul-2012.) $)
    ltrhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
      ( vy ctopgrp wcel wa chomeosm co cuni wf1o ccn ccnv w3a wceq crn
      topgrpbs syl5eq adantr cgr topgrpgrp ltrooo sylan wb f1oeq23 anidms
      eqcoms syl5ibr mpcom cv cop cmpt ccom copab ltrcmp df-mpt eqcomi a1i
      coeq2d eqtrd ctop ctx topgrptop eqid txtop syl11anc wi eqtr ttcn2 eleq2
      anbi2d id eqidd mpteq12dv eleq1d imbi12d mpbiri syl mpan topgrpcn cnco
      syl32anc eqeltrd cgn cfv cgs ltrinvlem grpoinvcl syl5req eleqtrrd anbi1d
      opabbidv mpbird 3jca cvv ltrset hmeobc syl111anc ) FMNZBGNZOZCEEPQNZERZXK
      CSZCEETQZNZCUAZXMNZUBZXIXLXNXPGXKUCZXIXLXGXRXHXGGDUDZXKHDEFJKUEZUFUGZXIXL
      XRGGCSZXGDUHNZXHYBDFJUIZABCDGIHUJUKXLYBULZXKGXKGUCZYEXKGXKGCUMUNUOUPUQXIC
      DAGBAURZUSZUTZVAZXMXGYCXHCYJUCYDYCXHOZCDYGGNZLURZYHUCOALVBZVAYJALBCDGIHVC
      YKYNYIDYNYIUCYKYIYNALGYHVDVEVFVGVHUKXIEVINZEEVJQZVINZYOYIEYPTQZNZDYPETQNZ
      YJXMNXGYOXHEFKVKZUGZXGYQXHXGYOYOYQUUAUUAEEYPYPVLVMVNUGZUUBXSXKUCZXIYSXGUU
      DXHXTUGGXSUCZUUDXIYSVOZHUUEUUDOXRUUFGXSXKVPXRUUFXGBXKNZOZAXKYHUTZYRNZVOXG
      YOUUGUUJUUAALBUUIEXKXKVLZALXKYHVDVQUKXRXIUUHYSUUJXRXHUUGXGGXKBVRVSXRYIUUI
      YRXRAGYHXKYHXRVTXRYHWAWBWCWDWEWFWGUQXGYTXHDEFJKWHUGZYIDEYPEWIWJWKXIXOAGBD
      WLWMZWMZYGDQUTZXMXGYCXHXOUUOUCYDABDWNWMZCDUUMGIHUUPVLUUMVLZWOUKXIUUODYLYM
      UUNYGUSUCZOZALVBZVAZXMXIYCUUNGNZUUOUVAUCXGYCXHYDUGZXGYCXHUVBYDBDUUMGHUUQW
      PUKZALUUNUUODGUUOVLHVCVNXIYOYQYOUUTYRNZYTUVAXMNUUBUUCUUBXIUVEYGXKNZUUROZA
      LVBZYRNZXIYOUUNXKNUVIUUBXIUUNGXKUVDXGYFXHXGGXSXKHXTWQUGWRALUUNUVHEXKUUKUV
      HVLVQVNXIXRUVEUVIULYAXRUUTUVHYRXRUUSUVGALXRYLUVFUURGXKYGVRWSWTWCWFXAUULUU
      TDEYPEWIWJWKWKXBXIYOYOCXCNZXJXQULUUBUUBXIYCUVJUVCABCDGIHXDWFXCCEEXKXKUUKU
      UKXEXFXA $.
      $( [12-Jul-2012] $)
  $}


  ${
    $d A x y $.  $d B x y $.  $d G x y $.  $d J y $.  $d K x $.  $d X x y $.
    lrtrhom.1 $e |- X = ran G $.
    lrtrhom.2 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    lrtrhom.3 $e |- G = ( 1st ` K ) $.
    lrtrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a right and left translation is a
       homeomorphism.  Note that ` A ` and ` B ` are constant.  (Contributed by
       FL, 12-Jul-2012.) $)
    lrtrhom $p |- ( ( K e. TopGrp /\ A e. X /\ B e. X )
       -> F e. ( J Homeo J ) ) $=
      ( vy ctopgrp wcel w3a cv co cmpt ccom chomeosm cgr wceq topgrpgrp eqid
      cmperltr syl3an1 ctop wa topgrptop 3jca 3ad2ant1 trhom 3adant2 ltrhom
      3adant3 jca cmphmp sylc eqeltrd ) GNOZBHOZCHOZPZDMHBMQERSZAHAQCERSZTZFFUA
      RZVAEUBOVBVCDVGUCEGKUDAMBCVEDEVFHJVEUEZVFUEZIUFUGVDFUHOZVKVKPZVFVHOZVEVHO
      ZUIVGVHOVAVBVLVCVAVKVKVKFGLUJZVOVOUKULVDVMVNVAVCVMVBACVFEFGHIVJKLUMUNVAVB
      VNVCMBVEEFGHIVIKLUOUPUQVFVEFFFURUSUT $.
      $( [12-Jul-2012] $)
  $}

  ${
    $d A o u v x $.  $d B o u v x $.  $d G a b o u v $.  $d X a b o u v x $.
    grphmlem0.1 $e |- H = { o | E. a e. X E. b e. X
         o = ( x e. X |-> ( ( a G x ) G b ) ) } $.
    $( Condition for a left and right translation to belong to the class of all
       the left and right translations over the class ` X ` .  (Contributed by
       FL, 16-Jul-2012.) $)
    grphmlem0 $p |- ( ( A e. X /\ B e. X /\ X e. C )
       -> ( x e. X |-> ( ( A G x ) G B ) ) e. H ) $=
      ( vu vv wcel w3a cv co cmpt wceq wrex cab eqid opreq1 opreq1d adantr
      mpteq2dv eqeq2d opreq2 rcla42ev mp3an3 3adant3 cvv wb mptexg 3ad2ant3
      eqeq1 rexbidva elabg syl mpbird weq wa simpl rexbidv opreq2d cbvrexv
      syl6bb abbii syl6eleqr ) BHNZCHNZHDNZOZAHBAPZFQZCFQZRZEPZAHIPZVNFQZJPZFQZ
      RZSZJHTZIHTZEUAZGVMVQVRAHLPZVNFQZMPZFQZRZSZMHTZLHTZEUAZWGVMVQWPNZVQWLSZMH
      TZLHTZVJVKWTVLVJVKVQVQSZWTVQUBWRXAVQAHVOWJFQZRZSLMBCHHWHBSZWLXCVQXDAHWKXB
      XDWKXBSVNHNZXDWIVOWJFWHBVNFUCUDUEUFUGWJCSZXCVQVQXFAHXBVPXFXBVPSXEWJCVOFUH
      UEUFUGUIUJUKVMVQULNZWQWTUMVLVJXGVKAHVPDUNUOWOWTEVQULVRVQSZWNWSLHXHWNWSUMW
      HHNXHWMWRMHXHWMWRUMWJHNVRVQWLUPUEUQUEUQURUSUTWFWOEWEWNILHILVAZWEVRAHWIWAF
      QZRZSZJHTWNXIWDXLJHXIWCXKVRXIAHWBXJXIXEVBZVTWIWAFXMVSWHVNFXIXEVCUDUDUFUGV
      DXLWMJMHJMVAZXKWLVRXNAHXJWKXNXEVBWAWJWIFXNXEVCVEUFUGVFVGVFVHVIKVI $.
      $( [16-Jul-2012] $)
  $}

  ${
    grphm.1 $e |- X = ran G $.
    grphm.2 $e |- G = ( 1st ` K ) $.
    grphm.3 $e |- J = ( 2nd ` K ) $.
    grphm.4 $e |- H = { o | E. a e. X E. b e. X
         o = ( p e. X |-> ( ( a G p ) G b ) ) } $.

    ${
      $d G a b c d e f o p $.  $d G c d e f p q $.  $d G c d p q z $.
      $d H c d e f y $.  $d K c d e f x y $.  $d X a b c d e f o p $.
      $d X c d e f p q $.  $d o x y $.
      $( Lemma for ~ grphm .  Closure. $)
      grphmlem1 $p |- ( K e. TopGrp
       -> A. x e. H
             A. y e. H ( x o. y ) e. H ) $=
        ( vc vd ve vf vq vz ctopgrp wcel cv ccom wa co cmpt wceq wrex cab wi
        eleq2i anbi12i weq opreq1 opreq1d adantr mpteq2dv eqeq2d opreq2
        cbvrex2v abbii simpl opreq2d bitri ax-17 visset eqeq1d 2rexbidva elabf
        wb eqeq1 cbvmpt eqeq2i w3a cgr topgrpgrp 3ad2ant3 anim12i ancoms
        3adant3 simpr eqid cmprltr syl111anc simp2l simp1l grpocl simp1r
        simp2r eqidd rcla42ev cvv crn c1st cfv fvex eqeltri rnex mptexg ax-mp
        a1i elab3 sylibr syl6eleqr eqeltrd 3exp coeq12d eleq1d imbi2d syl5ibr
        ex sylbi com4l com12 r19.23aivv com3l imp syl2anb r19.21aivv ) GUBUCZAU
        DZBUDZUEZEUCZABEEYCEUCZYDEUCZUFZYBYFYIYCCUDZIHPUDZIUDZDUGZQUDZDUGZUHZUI
        ZQHUJPHUJZCUKZUCZYDYJIHRUDZYLDUGZSUDZDUGZUHZUIZSHUJRHUJZCUKZUCZUFZYBYFU
        LZYIYCYJIHJUDZYLDUGZKUDZDUGZUHZUIZKHUJJHUJZCUKZUCZYDUUSUCZUFUUJYGUUTYHU
        VAEUUSYCOUMEUUSYDOUMUNUUTYTUVAUUIUUSYSYCUURYRCUUQYQYJIHYMUUNDUGZUHZUIJK
        PQHHJPUOZUUPUVCYJUVDIHUUOUVBUVDUUOUVBUIYLHUCZUVDUUMYMUUNDUULYKYLDUPUQUR
        USUTKQUOZUVCYPYJUVFIHUVBYOUVFUVBYOUIUVEUUNYNYMDVAURUSUTVBVCUMUUSUUHYDUU
        RUUGCUUQUUFYJIHUUBUUNDUGZUHZUIJKRSHHJRUOZUUPUVHYJUVIIHUUOUVGUVIUVEUFUUM
        UUBUUNDUVIUUMUUBUIUVEUULUUAYLDUPURUQUSUTKSUOZUVHUUEYJUVJIHUVGUUDUVJUVEU
        FUUNUUCUUBDUVJUVEVDVEUSUTVBVCUMUNVFYTYCYPUIZQHUJPHUJZYDUUEUIZSHUJRHUJZU
        UKUUIYRUVLCYCUVLCVGAVHCAUOZYQUVKPQHHUVOYKHUCZYNHUCZUFZUFYJYCYPUVOUVRVDV
        IVJVKUUGUVNCYDUVNCVGBVHCBUOZUUFUVMRSHHUVSUUFUVMVLUUAHUCZUUCHUCZUFZYJYDU
        UEVMURVJVKUVLUVNUUKUVKUVNUUKULPQHHUVNUVRUVKUUKUVMUVRUVKUUKULULZRSHHUVMU
        WBUWCUVKUVMUWBUVRUUKUVKYCTHYKTUDZDUGZYNDUGZUHZUIZUVMUWBUVRUUKULZULZULYP
        UWGYCITUAHYOUWFUAUDZYOUCTVGUWKUWFUCIVGITUOYMUWEYNDYLUWDYKDVAUQVNVOUWHUV
        MUWJUWBUWIUWHUVMUFZUVRYBUWGUUEUEZEUCZULZULUWBUVRYBUWNUWBUVRYBVPZUWMIHYK
        UUADUGZYLDUGZUUCYNDUGZDUGZUHZEUWPDVQUCZUVPUVTUFZUVQUWAUFZUWMUXAUIYBUWBU
        XBUVRDGMVRVSZUWBUVRUXCYBUVRUWBUXCUVRUVPUWBUVTUVPUVQVDUVTUWAVDVTWAWBUWBU
        VRUXDYBUVRUWBUXDUVRUVQUWBUWAUVPUVQWCUVTUWAWCVTWAWBITYKUUAYNUUCUUEUWGDHU
        WGWDUUEWDLWEWFUWPUXAUUSEUWPUXAUUPUIZKHUJJHUJZUXAUUSUCUWPUWQHUCZUWSHUCZU
        XAUXAUIZUXGUWPUXBUVPUVTUXHUXEUWBUVPUVQYBWGUVTUWAUVRYBWHYKUUADHLWIWFUWPU
        XBUWAUVQUXIUXEUVTUWAUVRYBWJUWBUVPUVQYBWKUUCYNDHLWIWFUWPUXAWLUXFUXJUXAIH
        UWRUUNDUGZUHZUIJKUWQUWSHHUULUWQUIZUUPUXLUXAUXMIHUUOUXKUXMUUOUXKUIUVEUXM
        UUMUWRUUNDUULUWQYLDUPUQURUSUTUUNUWSUIZUXLUXAUXAUXNIHUXKUWTUXNUVEUFUUNUW
        SUWRDUXNUVEVDVEUSUTWMWFUURUXGCUXAUXAWNUCZUXGHWNUCUXOHDWOWNLDDGWPWQWNMGW
        PWRWSWTWSIHUWTWNXAXBXCYJUXAUIZUUQUXFJKHHUXPUULHUCUUNHUCUFZUFYJUXAUUPUXP
        UXQVDVIVJXDXEOXFXGXHUWLUUKUWOUVRUWLYFUWNYBUWLYEUWMEUWLYCUWGYDUUEUWHUVMV
        DUWHUVMWCXIXJXKXKXLXMXNXOXPXQXRXQXSXTXNXPYA $.
        $( [17-Jul-2012] $) $( [26-Jun-2012] $)
    $}

    ${
      $d G a b c d e i p o $.  $d H g h x y z $.  $d K a b c d e f g h i p $.
      $d X a b c d e i p o $.  $d f g h o x y z $.
      $( Lemma for ~ grphm .  Associativity . $)
      grphmlem2 $p |- ( K e. TopGrp -> A. f e. H A. g e. H A. h e. H (
      ( f { <. <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x
      o. y ) ) } g ) { <. <. x , y >. , z >. | ( x e. H /\ y e. H
      /\ z = ( x o. y ) ) } h ) = ( f { <. <. x , y >. , z >. | ( x
      e. H /\ y e. H /\ z = ( x o. y ) ) } ( g { <. <. x , y >. , z
      >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) } h ) ) ) $=
        ( vc vd ve vi ctopgrp wcel cv ccom wceq w3a copab2 co wral wa wi cmpt
        wrex cab visset weq simpl eqeq1d 2rexbidva elab eleq2i eqeq1 rexbidv
        opreq1 opreq1d adantr mpteq2dv eqeq2d opreq2 cbvrex2v cgr topgrpgrp
        adantl anim12i simpr 3jca eqid cmprltr2 syl coeq1d ad2antlr simplll
        simplrl grpocl syl111anc simplrr simpllr simprr syl112anc simprl
        syl122anc coeq2d grpoass syl11anc jca eqcomd opreq12d 3eqtr4rd 3eqtrd
        coeq2 coeq1 eqeq12d syl5ibr imp expdimp exp3a exp4c ex com14 3imp
        impcom com12 r19.23aivv 3exp com3l syl2anb sylbi eleq2s r19.21aiva
        r19.21aivva ax-17 cvv grphmlem1 ra42 coex a1i df-3an oprabbii
        oprabval2g imp31 ralbida 2ralbida mpbird coexg opreqi syl5eq opreq2d )
        KUDUEZDUFZEUFZAUFZIUEZBUFZIUEZCUFUUDUUFUGZUHZUIZABCUJZUKZFUFZUUKUKZUUBU
        UCUUMUUKUKZUUKUKZUHZFIULZEIULDIULUUBUUCUGZUUMUUKUKZUUBUUCUUMUGZUUKUKZUH
        ZFIULZEIULDIULZUUAUVEUUSUUMUGZUUBUVAUGZUHZFIULZEIULDIULUUAUVIDEIIUUAUUB
        IUEZUUCIUEZUMZUMZUVHFIUUMIUEZUVMUVHUVMUVHUNZUUMGUFZMLNUFZMUFZHUKZOUFZHU
        KZUOZUHZOLUPZNLUPZGUQZIUUMUWFUEUUMUWBUHZOLUPNLUPZUVOUWEUWHGUUMFURZGFUSZ
        UWCUWGNOLLUWJUVQLUEZUVTLUEZUMZUMUVPUUMUWBUWJUWMUTVAVBVCUVMUWHUVHUVLUUAU
        WHUVHUNZUVJUUBUWFUEZUUCUWFUEZUUAUWNUNZUVKIUWFUUBSVDIUWFUUCSVDUWOUUBUWBU
        HZOLUPZNLUPZUUCUWBUHZOLUPZNLUPZUWQUWPUWEUWTGUUBDURZGDUSZUWDUWSNLUXEUWCU
        WROLUVPUUBUWBVEVFVFVCUWEUXCGUUCEURZGEUSZUWDUXBNLUXGUWCUXAOLUVPUUCUWBVEV
        FVFVCUWTUUBMLTUFZUVRHUKZUAUFZHUKZUOZUHZUALUPTLUPZUUCMLUBUFZUVRHUKZUCUFZ
        HUKZUOZUHZUCLUPUBLUPZUWQUXCUWRUXMUUBMLUXIUVTHUKZUOZUHNOTUALLNTUSZUWBUYC
        UUBUYDMLUWAUYBUYDUWAUYBUHUVRLUEZUYDUVSUXIUVTHUVQUXHUVRHVGVHVIVJVKOUAUSZ
        UYCUXLUUBUYFMLUYBUXKUYFUYBUXKUHUYEUVTUXJUXIHVLVIVJVKVMUXAUXTUUCMLUXPUVT
        HUKZUOZUHNOUBUCLLNUBUSZUWBUYHUUCUYIMLUWAUYGUYIUWAUYGUHUYEUYIUVSUXPUVTHU
        VQUXOUVRHVGVHVIVJVKOUCUSZUYHUXSUUCUYJMLUYGUXRUYJUYGUXRUHUYEUVTUXQUXPHVL
        VIVJVKVMUXNUYAUWQUXMUYAUWQUNTUALLUYAUXHLUEZUXJLUEZUMZUXMUWQUXTUYMUXMUWQ
        UNZUNUBUCLLUXOLUEZUXQLUEZUMZUXTUYMUYNUYQUXTUYMUIZUXMUUAUWNUWHUYRUXMUUAU
        IZUVHUWGUYSUVHUNZNOLLUWGUWMUYTUWGUWMUYSUVHUWMUYSUMUVHUWGUUSUWBUGZUUBUUC
        UWBUGZUGZUHZUYSUWMVUDUYRUXMUUAUWMVUDUNZUYQUXTUYMUXMUUAVUEUNZUNUXMUXTUYM
        UYQVUFUXMUXTUYMUYQVUFUNUNUXMUXTUMZUYMUYQUUAVUEVUGUYMUYQUMZUUAUMZUWMVUDU
        XMUXTVUIUWMUMZVUDUXTVUJUMVUDUXMUXLUUCUGZUWBUGZUXLVUBUGZUHZUXTVUJVUNVUJV
        UNUXTUXLUXSUGZUWBUGZUXLUXSUWBUGZUGZUHVUJVUPMLUXHUXOHUKZUVRHUKUXQUXJHUKZ
        HUKUOZUWBUGZMLVUSUVQHUKZUVRHUKZUVTVUTHUKZHUKZUOZVURVUJVUOVVAUWBVUJHVNUE
        ZUYKUYOUMZUYLUYPUMZUIZVUOVVAUHVUIVVKUWMVUIVVHVVIVVJUUAVVHVUHHKQVOZVPZVU
        HVVIUUAUYMUYKUYQUYOUYKUYLUTUYOUYPUTVQVIVUHVVJUUAUYMUYLUYQUYPUYKUYLVRUYO
        UYPVRVQVIVSVIMMUXHUXOUXJUXQUXSUXLHLUXLVTZUXSVTZPWAWBWCVUJVVHVUSLUEZUWKU
        MVUTLUEZUWLVVBVVGUHUUAVVHVUHUWMVVLWDZVUIVVPUWMUWKVUIVVHUYKUYOVVPVVMUYKU
        YLUYQUUAWEZUYMUYOUYPUUAWFZUXHUXOHLPWGWHUWKUWLUTVQVUJVVHUYPUYLVVQVVRVUIU
        YPUWMUYMUYOUYPUUAWIVIZVUIUYLUWMUYKUYLUYQUUAWJVIZUXQUXJHLPWGWHVUIUWKUWLW
        KZMMVUSUVQVUTUVTUWBVVAHLVVAVTUWBVTZPWAWLVUJUXLMLUXOUVQHUKZUVRHUKUVTUXQH
        UKZHUKUOZUGZMLUXHVWEHUKZUVRHUKZVWFUXJHUKZHUKZUOZVURVVGVUJVVHUYKVWELUEZU
        YLVWFLUEZVWHVWMUHVVRVUIUYKUWMVVSVIZVUJVVHUYOUWKVWNVVRVUIUYOUWMVVTVIZVUI
        UWKUWLWMZUXOUVQHLPWGWHVWBVUJVVHUWLUYPVWOVVRVWCVWAUVTUXQHLPWGWHMMUXHVWEU
        XJVWFVWGUXLHLVVNVWGVTPWAWNVUJVUQVWGUXLVUJVVHUYOUWKUYPUWLVUQVWGUHVVRVWQV
        WRVWAVWCMMUXOUVQUXQUVTUWBUXSHLVVOVWDPWAWNWOVUJMLVVFVWLVUJUYEUMZVVDVWJVV
        EVWKHVWSVVCVWIUVRHVWSVVHUYKUYOUWKUIZVVCVWIUHVUJVVHUYEVVRVIVUJVWTUYEVUJU
        YKUYOUWKVWPVWQVWRVSVIUXHUXOUVQHLPWPWQVHVWSVVHUWLUYPUYLUIZUMZVVEVWKUHVUJ
        VXBUYEVUJVVHVXAVVRVUJUWLUYPUYLVWCVWAVWBVSWRVIVXBVWKVVEUVTUXQUXJHLPWPWSW
        BWTVJXAXBUXTVULVUPVUMVURUXTVUKVUOUWBUUCUXSUXLXCWCUXTVUBVUQUXLUUCUXSUWBX
        DWOXEXFXGUXMVUAVULVUCVUMUXMUUSVUKUWBUUBUXLUUCXDWCUUBUXLVUBXDXEXFXHXIXJX
        KXLXMXMXNUWGUVFVUAUVGVUCUUMUWBUUSXCUWGUVAVUBUUBUUMUWBUUCXCWOXEXFXIXOXPX
        OXQXQXPXRXPXGXSXSXSXNXOXTSYAXNYBYCUUAUVDUVIDEIIUUADYDZUUAEYDZUVMUVCUVHF
        IUVMFYDZUVMUVNUMZUUTUVFUVBUVGVXFUUSIUEZUVNUVFYEUEZUUTUVFUHUVMVXGUVNUUAU
        VLVXGUUAVXGEIULDIULUVLVXGUNDEGHIJKLMNOPQRSYFVXGDEIIYGWBXGVIUVMUVNVRZVXH
        VXFUUSUUMUUBUUCUXDUXFYHUWIYHYIABCUUSUUMIIUUHUVFUUKUUSUUFUGYEUUDUUSUUFXD
        UUFUUMUUSXCUUJUUEUUGUMUUIUMZABCUUEUUGUUIYJYKZYLWHVXFUVJUVAIUEZUVGYEUEZU
        VBUVGUHUUAUVJUVKUVNWFZUUAUVLUVNVXLUVLUUAUVNVXLUNZUVKUUAVXOUNUVJUUAUVKVX
        OUUAUVKUVNVXLUUAVXLFIULEIULUVKUVNUMVXLUNEFGHIJKLMNOPQRSYFVXLEFIIYGWBXIX
        OVPXOYMVXMVXFUUBUVAUXDUUCUUMUXFUWIYHZYHYIABCUUBUVAIIUUHUVGUUKUUBUUFUGZY
        EUUDUUBUUFXDZUUFUVAUUBXCVXKYLWHXEYNYOYPUUAUURUVDDEIIVXCVXDUVMUUQUVCFIVX
        EVXFUUNUUTUUPUVBVXFUULUUSUUMUUKVXFUVJUVKUUSYEUEZUULUUSUHVXNUUAUVJUVKUVN
        WIZUVLVXSUUAUVNUUBUUCIIYQWDABCUUBUUCIIUUHUUSUUKVXQYEVXRUUFUUCUUBXCVXKYL
        WHVHVXFUUOUVAUUBUUKVXFUVKUVNUVAYEUEZUUOUVAUHVXTVXIVYAVXFVXPYIUVKUVNVYAU
        IUUOUUCUUMVXJABCUJZUKUVAUUKVYBUUCUUMVXKYRABCUUCUUMIIUUHUVAVYBUUCUUFUGYE
        UUDUUCUUFXDUUFUUMUUCXCVYBVTYLYSWHYTXEYNYOYP $.
        $( [29-Jun-2012] $)
    $}

    ${
      $d G a b f o p u v x y z $.  $d H f x y z $.  $d K a b f p $.
      $d X a b f o p u x y z $.

      $( Lemma for ~ grphm .  Neutral element . $)
      grphlem3 $p |- ( ( K e. TopGrp /\ ( x e. X |-> ( ( ( Id ` G ) G
                         x ) G ( Id ` G ) ) ) e. H ) -> A. f e. H ( (
                         x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G )
                         ) ) { <. <. x , y >. , z >. | ( x e. H /\
                         y e. H /\ z = ( x o. y ) ) } f ) = f ) $=
        ( vu vv cgi cfv cv co cmpt wcel ctopgrp ccom wceq w3a copab2 wral
        ax-17 weq opreq2 opreq1d cbvmpt eleq1i wa wi opreq1i cvv wrex cab cgr
        topgrpgrp eqid grpoidcl syl adantr a1i simpl mpteq2dv eqeq2d opreq2d
        rcla42ev syl111anc crn c1st fvex eqeltri rnex mptexg ax-mp eqeq1
        rexbidv elab sylibr syl6eleqr simpr visset coex coeq1 coeq2 df-3an
        oprabbii oprabval2g adantl simpll simplr cmprltr2 syl122anc eqidd
        grpolid ex impcom adantlr grporid com12 imp opreq12d mpteq12dv eqtrd
        id eqeq12d imbi2d syl5ibrcom r19.23aivv sylbi eleq2s syl5eq r19.21aiv
        sylan2b ) AJFTUAZAUBZFUCZYCFUCZUDZGUEIUFUEZRJYCRUBZFUCZYCFUCZUDZGUEZYGD
        UBZYDGUEZBUBZGUEZCUBYDYPUGZUHZUIZABCUJZUCZYNUHZDGUKYGYLGARSJYFYKSUBZYFU
        ERULUUDYKUEZAULARUMYEYJYCFYDYIYCFUNUOUPZUQYHYMURUUCDGYHYNGUEZUUCUSYMYHU
        UGUUCYHUUGURZUUBYLYNUUAUCZYNYGYLYNUUAUUFUTUUHUUIYLYNUGZYNUUHYMUUGUUJVAU
        EZUUIUUJUHUUHYLEUBZKJLUBZKUBZFUCZMUBZFUCZUDZUHZMJVBZLJVBZEVCZGUUHYLUURU
        HZMJVBZLJVBZYLUVBUEUUHYCJUEZUVFYLKJYCUUNFUCZYCFUCZUDZUHZUVEYHUVFUUGYHFV
        DUEZUVFFIOVEZYCFJNYCVFZVGVHZVIZUVOUVJUUHRKSJYKUVHUUEKULUUDUVHUERULRKUMY
        JUVGYCFYIUUNYCFUNUOUPVJUVCUVJYLKJUVGUUPFUCZUDZUHLMYCYCJJUUMYCUHZUURUVQY
        LUVRKJUUQUVPUVRUUNJUEZURZUUOUVGUUPFUVTUUMYCUUNFUVRUVSVKUOUOVLVMUUPYCUHZ
        UVQUVIYLUWAKJUVPUVHUWAUVSURUUPYCUVGFUWAUVSVKVNVLVMVOVPUVAUVEEYLJVAUEYLV
        AUEJFVQVANFFIVRUAVAOIVRVSVTWAVTRJYKVAWBWCZUULYLUHZUUTUVDLJUWCUUSUVCMJUU
        LYLUURWDWEWEWFWGQWHYHUUGWIUUKUUHYLYNUWBDWJZWKVJABCYLYNGGYRUUJUUAYLYPUGV
        AYDYLYPWLYPYNYLWMYTYOYQURYSURABCYOYQYSWNWOWPVPUUGYHUUJYNUHZYHUWEUSZYNUV
        BGYNUVBUEYNUURUHZMJVBZLJVBZUWFUVAUWIEYNUWDEDUMZUUTUWHLJUWJUUSUWGMJUULYN
        UURWDWEWEWFUWGUWFLMJJUUMJUEZUUPJUEZURZUWFUWGYHYLUURUGZUURUHZUSUWMYHUWOU
        WMYHURZUWNKJYCUUMFUCZUUNFUCZUUPYCFUCZFUCZUDZUURUWPUVKUVFUWKUVFUWLUWNUXA
        UHYHUVKUWMUVLWQYHUVFUWMUVNWQZUWKUWLYHWRUXBUWKUWLYHWSKRYCUUMYCUUPUURYLFJ
        YLVFUURVFNWTXAUWPKJUWTJUUQUWPJXBUWPUWRUUOUWSUUPFUWKYHUWRUUOUHUWLUWKYHUR
        UWQUUMUUNFYHUWKUWQUUMUHZYHUVKUWKUXCUSUVLUVKUWKUXCUUMYCFJNUVMXCXDVHXEUOX
        FUWMYHUWSUUPUHZUWLYHUXDUSUWKYHUWLUXDYHUVKUWLUXDUSUVLUVKUWLUXDUUPYCFJNUV
        MXGXDVHXHWQXIXJXKXLXDUWGUWEUWOYHUWGUUJUWNYNUURYNUURYLWMUWGXMXNXOXPXQXRQ
        XSXEXLXTXDVIYAYB $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d G o p $.  $d H h $.  $d J a b h o $.  $d K a b h $.  $d K a b p $.
      $d X b o p $.
      $( Lemma for ~ grphm .  Homeomorphisms . $)
      grphmlem4 $p |- ( K e. TopGrp -> H C_ ( J Homeo J ) ) $=
        ( vh ctopgrp wcel chomeosm co cv cuni wf1o cima wral ccnv w3a wi cmpt
        wceq wrex cab visset weq eqeq1 rexbidv elab wa crn topgrpbs eqtr cgr
        topgrpgrp eqid rltrooo 3exp syl wb f1oeq23 anidms imbi2d syl5ibr
        eqcoms mpan mpcom com3l 3imp ctop topgrptop jca 3ad2ant3 lrtrhom 3coml
        hmeocnb sylc hmeocna 3jca 3expia f1oeq1 imaeq1 eleq1d ralbidv cnveq
        imaeq1d 3anbi123d syl5ibrcom r19.23aivv sylbi eleq2s com12 cvv a1i
        ishomeo syl111anc sylibrd ssrdv ) EOPZNCDDQRZXENSZCPZDTZXIXGUAZXGASZUBZ
        DPZADUCZXGUDZXKUBZDPZADUCZUEZXGXFPZXHXEXSXEXSUFZXGXKGFHSZGSBRISZBRUGZUH
        ZIFUIZHFUIZAUJZCXGYHPXGYDUHZIFUIZHFUIZYAYGYKAXGNUKZANULZYFYJHFYMYEYIIFX
        KXGYDUMUNUNUOYIYAHIFFYBFPZYCFPZUPYAYIXEXIXIYDUAZYDXKUBZDPZADUCZYDUDZXKU
        BZDPZADUCZUEZUFYNYOXEUUDYNYOXEUEZYPYSUUCYNYOXEYPXEYNYOYPBUQZXIUHZXEYNYO
        YPUFZUFZBDEKLURFUUFUHZUUGXEUUIUFZJUUJUUGUPFXIUHUUKFUUFXIUSUUKXIFXEUUIXI
        FUHZYNYOFFYDUAZUFZUFZXEBUTPZUUOBEKVAUUPYNYOUUMGYBYCYDBFYDVBZJVCVDVEUULU
        UHUUNYNUULYPUUMYOUULYPUUMVFXIFXIFYDVGVHVIVIVJVKVEVLVMVNVOUUEDVPPZUURUPZ
        YDXFPZYSXEYNUUSYOXEUURUURDELVQZUVAVRVSXEYNYOUUTGYBYCYDBDEFJUUQKLVTWAZAY
        DDDWBWCUUEUUSUUTUUCUUEUURUURXEYNUURYOUVAVSZUVCVRUVBAYDDDWDWCWEWFYIXSUUD
        XEYIXJYPXNYSXRUUCXIXIXGYDWGYIXMYRADYIXLYQDXGYDXKWHWIWJYIXQUUBADYIXPUUAD
        YIXOYTXKXGYDWKWLWIWJWMVIWNWOWPMWQWRXEUURUURXGWSPZXTXSVFUVAUVAUVDXEYLWTA
        WSXGDDXIXIXIVBZUVEXAXBXCXD $.
        $( [12-Jul-2012] $)
    $}

    ${
      $d G a b g p u x y z $.  $d G a b o p u $.  $d G b g p u v x y z $.
      $d G p u v w x $.  $d H g u v x y z $.  $d K f u v $.  $d K u v x $.
      $d X a b g p u x y z $.  $d X a b o p u $.  $d X b g p u v x y z $.
      $d a b f g u $.  $d f o u v $.
      $( Lemma for ~ grphm .  Inverse element . $)
      grphlem5 $p |- ( K e. TopGrp -> A. f e. H E. g e. H ( g { <.
      <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) }
      f ) = ( x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G ) ) ) ) $=
        ( vu vv vw ctopgrp wcel cv ccom wceq w3a copab2 co cgi cfv cmpt wrex
        wi cab visset weq wb wa simpl eqeq1d rexbidva adantr elab opreq1d
        mpteq2dv eqeq2d opreq2d cbvrex2v cgn cvv cgr topgrpgrp adantl simpll
        eqid grpoinvcl syl11anc simplr crn rnexg syl syl5eqel grphmlem0
        syl111anc oprabval2gc cmprltr2 syl122anc ax-17 opreq2 cbvmpt grpolinv
        ad2antlr simpllr grporinv opreq12d syl5eq 3eqtrd opreq1 rcla4ev expcom
        com12 rexbidv imbi2d syl5ibr com3l mpcom ex com23 r19.23aivv sylbi
        eleq2s r19.21aiv ) JUBUCZEUDZDUDZAUDZHUCBUDZHUCCUDXQXRUEUFUGABCUHZUIZAK
        GUJUKZXQGUIZYAGUIZULZUFZEHUMZDHXPHUCXNYFXNYFUNZXPFUDZLKMUDZLUDZGUIZNUDZ
        GUIZULZUFZNKUMZMKUMZFUOZHXPYRUCXPYNUFZNKUMZMKUMZYGYQUUAFXPDUPFDUQZYPYTM
        KUUBYPYTURYIKUCUUBYOYSNKUUBYLKUCZUSYHXPYNUUBUUCUTVAVBVCVBVDUUAXPLKSUDZY
        JGUIZTUDZGUIZULZUFZTKUMSKUMYGYSUUIXPLKUUEYLGUIZULZUFMNSTKKMSUQZYNUUKXPU
        ULLKYMUUJUULYJKUCZUSZYKUUEYLGUUNYIUUDYJGUULUUMUTVEVEVFVGNTUQZUUKUUHXPUU
        OLKUUJUUGUUOUUMUSYLUUFUUEGUUOUUMUTVHVFVGVIUUIYGSTKKUUDKUCZUUFKUCZUSZXNU
        UIYFUURXNUUIYFUNZLKUUDGVJUKZUKZYJGUIUUFUUTUKZGUIULZHUCZUURXNUSZUUSUVEUV
        AKUCZUVBKUCZKVKUCZUVDUVEGVLUCZUUPUVFXNUVIUURGJPVMZVNZUUPUUQXNVOZUUDGUUT
        KOUUTVPZVQVRZUVEUVIUUQUVGUVKUUPUUQXNVSZUUFGUUTKOUVMVQVRZUVEKGVTZVKOUVEU
        VIUVQVKUCUVKGVLWAWBWCZLUVAUVBVKFGHKMNRWDWEZUUIUVDUVEYFUVDUVEYFUNUUIUVEX
        OUUHXSUIZYDUFZEHUMZUNUVEUVDUWBUVEUVCUUHXSUIZYDUFZUVDUWBUNUVEUWCUVCUUHUE
        ZLKUVAUUDGUIZYJGUIZUUFUVBGUIZGUIZULZYDUVEUVDUUHHUCZUWCUWEUFUVSUVEUUPUUQ
        UVHUWKUVLUVOUVRLUUDUUFVKFGHKMNRWDWEABCUVCUUHHHXSXSVPWFVRUVEUVIUVFUUPUVG
        UUQUWEUWJUFUVKUVNUVLUVPUVOLLUVAUUDUVBUUFUUHUVCGKUVCVPUUHVPOWGWHUVEUWJAK
        UWFXQGUIZUWHGUIZULYDLAUAKUWIUWMUAUDZUWIUCAWIUWNUWMUCLWILAUQUWGUWLUWHGYJ
        XQUWFGWJVEWKUVEAKUWMYCUVEXQKUCZUSZUWLYBUWHYAGUWPUWFYAXQGUVEUWFYAUFZUWOU
        VEUVIUUPUWQUVKUVLUUDYAGUUTKOYAVPZUVMWLVRVCVEUWPUVIUUQUWHYAUFXNUVIUURUWO
        UVJWMUUPUUQXNUWOWNUUFYAGUUTKOUWRUVMWOVRWPVFWQWRUVDUWDUWBUWAUWDEUVCHXOUV
        CUFUVTUWCYDXOUVCUUHXSWSVAWTXAWBXBUUIYFUWBUVEUUIYEUWAEHUUIXTUVTYDXPUUHXO
        XSWJVAXCXDXEXFXGXHXIXJXKXKRXLXBXM $.
        $( [16-Jul-2012] $)
    $}

    ${
      $d G a a0 a1 b b0 b1 o p x y $.  $d G a b f g o p u x y z $.
      $d G a0 a1 b0 b1 p q $.  $d G a b o p u v $.  $d G p w x $.
      $d H a b f g h o u x y z $.  $d J a b o $.
      $d K a a0 a1 b b0 b1 o p x y $.  $d K a b f g h o p u x y $.
      $d X a a0 a1 b b0 b1 o p x y $.  $d X a b f g o p u x y z $.
      $d X a0 a1 b0 b1 p q $.  $d X a b o p u v $.  $d a1 b b1 o p x y z $.
      $d q z $.
      $( The functions of equation ` ( ( a G p ) G b ) ` form a group of
         homeomorphisms.  (Contributed by FL, 17-Jul-2012.) $)
      grphm $p |- ( K e. TopGrp -> ( { <. <. x , y >. , z >. |
         ( x e. H /\ y e. H /\ z = ( x o. y ) ) } e. GrpOp /\
         H C_ ( J Homeo J ) ) ) $=
        ( vf vg vh vu vv bnjva1 bnjvb1 bnjva0 bnjvb0 vq vw ctopgrp wcel cv
        ccom wceq w3a copab2 cgr chomeosm co wss crn cxp wf wral wrex wa cmpt
        cab wex wb wal wi eqid weq opreq1 opreq1d adantr mpteq2dv eqeq2d
        opreq2 rcla42ev mp3an3 cvv topgrpgrp unsgrp syl adantl mptexg eqeq1
        rexbidv elabg mpbird cbvrexv syl6bb abbii syl6eleqr 3adant2 c1st cfv
        cgi grpoidcl eqcomi eleq1i simpl fveq2 eqcomd eleq1d syl5ibr ax-mp
        simpr grpocl syl111anc jca sylanb fveq2i grporid grpolid eqtr2d a1d
        eqidd mpteq12dv 3exp com3r syl6 com4t pm2.43i rneqi eqtr4i eleq2s
        mpcom impcom rnexg syl5eqel 3ad2ant3 simp2 simp1 3adant2r simp2r 3jca
        r19.21aiv fopab2a sylib crimmt1 eqtr4d ax-17 coeq2 rcla4e syl11anc
        coeq1 r2ex df-3an bicomi 2exbii bitri r19.23aivv com12 visset a1i
        opreq12d anbi12d ex cbvexd bnj1158 eleq2i simpll simplr 3bitri coeq12d
        simp2ll simp2rl simp2rr simp2lr anim12i cbvmpt coeq1d cmprltr eqtrd
        biimpd 3imp opreq2d syl6bi com4r com23 com3l sylbi 19.23advv impbid
        19.21aiv eq2ab sylibr 3anbi3d 2exbidv cbvabv syl6eq rnoprab 3eqtr4g
        grphmlem1 oprabbii foprab2 grphmlem2 fvex eqeltri rnex elab c2nd
        grphlem3 grphlem5 r19.26 sylanbrc eqeq1d eqeq2 ralbidv rcla4edv mpd
        mpdan xpeq12 anidms feq23 mpancom id raleqdv raleqbidv rexeqdv anbi2d
        rexeqbidv 3anbi123d eqcoms ab2rexex pm3.2i oprabex2g isgrpo grphmlem4
        ) HUHUIZAUJZFUIZBUJZFUIZCUJZUYLUYNUKZULZUMZABCUNZUOUIZFGGUPUQURUYKVUAUY
        TUSZVUBUTZVUBUYTVAZQUJZRUJZUYTUQSUJZUYTUQVUEVUFVUGUYTUQUYTUQULZSVUBVBZR
        VUBVBZQVUBVBZTUJZVUEUYTUQZVUEULZVUFVUEUYTUQZVULULZRVUBVCZVDZQVUBVBZTVUB
        VCZUMZFVUBULZUYKVVAUYKDUJZJIKUJZJUJZEUQZLUJZEUQZVEZULZLIVCZKIVCZDVFZUYS
        BVGAVGZCVFZFVUBUYKVVMUYMUYOVVCUYQULZUMZBVGAVGZDVFZVVOUYKVVLVVRVHZDVIVVM
        VVSULUYKVVTDUYKVVLVVRVVLUYKVVRVVJUYKVVRVJKLIIVVDIUIZVVGIUIZVDZVVJUYKVVR
        VWCVVJUYKUMZVVPBFVCZAFVCZVVRVWDVVIFUIZVVCVVIUYNUKZULZBFVCZVWFVWCUYKVWGV
        VJVWCUYKVDZVVIVVMFVWKVVIVVCJIVULVVEEUQZUAUJZEUQZVEZULZUAIVCZTIVCZDVFZVV
        MVWKVVIVWSUIZVVIVWOULZUAIVCZTIVCZVWCVXCUYKVWAVWBVVIVVIULZVXCVVIVKZVXAVX
        DVVIJIVVFVWMEUQZVEZULTUAVVDVVGIITKVLZVWOVXGVVIVXHJIVWNVXFVXHVWNVXFULVVE
        IUIZVXHVWLVVFVWMEVULVVDVVEEVMVNVOVPVQUALVLZVXGVVIVVIVXJJIVXFVVHVXJVXFVV
        HULVXIVWMVVGVVFEVRVOVPVQVSVTVOVWKVVIWAUIZVWTVXCVHVWKIWAUIZVXKUYKVXLVWCU
        YKEUOUIZVXLEHNWBZEIMWCWDWEJIVVHWAWFWDVWRVXCDVVIWAVVJVWQVXBTIVVJVWPVXAUA
        IVVCVVIVWOWGWHWHWIWDWJVVLVWRDVVKVWQKTIKTVLZVVKVVCJIVWLVVGEUQZVEZULZLIVC
        VWQVXOVVJVXRLIVXOVVIVXQVVCVXOJIVVHVXPVXOVVHVXPULVXIVXOVVFVWLVVGEVVDVULV
        VEEVMVNVOVPVQWHVXRVWPLUAILUAVLZVXQVWOVVCVXSJIVXPVWNVXSVXPVWNULVXIVVGVWM
        VWLEVRVOVPVQWKWLWKWMWNPWNWOVWDJIVVEVEZFUIVVCVVIVXTUKZULZVWJVWDVXTVVMFVW
        DVXTVVMUIZVXTVVIULZLIVCZKIVCZVWCUYKVYFVVJUYKVWCVYFUYKHWPWQZUOUIZVWCVYFV
        JZVYGHVYGVKWBVYGWRWQZVYGUSZUIVYHVYIVYJVYGVYKVYKVKVYJVKWSVYHVYIVJZVYJIVY
        KVYJIUIZVYLVYHVWCVYMVYMVYFVYHVWCVXTJIVYJVVEEUQZVYJEUQZVEZULZVYMVYMVYFVJ
        VJVYHVYQVWCVYHJIVVEVYOVYHVXIVDZVYOVYNVVEVYRVXMVYNIUIZVDZVYOVYNULVYHVXMV
        XIVYTVYGEUOEVYGNWTZXAZVXMVXIVDZVXMVYSVXMVXIXBZWUCVXMVYMVXIVYSWUDEVYGULZ
        WUCVYMVJNWUCVYMWUEEWRWQZIUIZVXMWUGVXIWUFEIMWUFVKWSZVOWUEVYJWUFIWUEWUFVY
        JEVYGWRXCXDXEXFXGVXMVXIXHVYJVVEEIMXIXJXKXLVYNVYJEIMVYGEWRWUAXMZXNWDVYHV
        XMVXIVYNVVEULWUBVVEVYJEIMWUIXOXLXPVPXQVYMVYMVYQVYFVYMVYMVYQVYFVYDVYQVXT
        JIVYNVVGEUQZVEZULKLVYJVYJIIVVDVYJULZVVIWUKVXTWULJIVVHIWUJWULIXRWULVVFVY
        NVVGEVVDVYJVVEEVMVNXSVQVVGVYJULZWUKVYPVXTWUMJIWUJIVYOWUMIXRVVGVYJVYNEVR
        XSVQVSXTYAYBYCYDVYKEUSZIVYGEWUAYEMYFYGYHWDYIWOVWDVXTWAUIZVYCVYFVHUYKVWC
        WUOVVJUYKVXLWUOUYKIWUNWAMUYKVXMWUNWAUIVXNEUOYJWDYKJIVVEWAWFWDYLVVLVYFDV
        XTWAVVCVXTULZVVKVYEKIWUPVVJVYDLIVVCVXTVVIWGWHWHWIWDWJPWNVWDVVCVVIVYAVWC
        VVJUYKYMVWDIIVVIVAZVYAVVIULVWCUYKWUQVVJVWKVVHIUIZJIVBWUQVWKWURJIVWKVXIV
        XMVVFIUIZVWBUMZWURUYKVWCVXIWUTVJZUYKVXMVWCWVAVJVXNVXMVWCVXIWUTVXMVWCVXI
        UMVXMWUSVWBVXMVWCVXIYNVXMVWAVXIWUSVWBVVDVVEEIMXIYOVXMVWAVWBVXIYPYQXTWDY
        IVVFVVGEIMXIYBYRJIIVVHVVIVXEYSYTWOJIIVVIUUAWDUUBVWIVYBBVXTFVYBBUUCUYNVX
        TULVWHVYAVVCUYNVXTVVIUUDVQUUEUUFVWEVWJAVVIFVWJAUUCUYLVVIULZVVPVWIBFWVBU
        YQVWHVVCUYLVVIUYNUUGVQWHUUEUUFVWFUYMUYOVDZVVPVDZBVGAVGVVRVVPABFFUUHWVDV
        VQABVVQWVDUYMUYOVVPUUIUUJUUKUULYTXTUUMUUNUYKVVQVVLABVVQUYKVVLUYMUYOVVPU
        YKVVLVJZUYOVVPWVEVJZVJZUYLVVMFUYLVVMUIZWVBLIVCZKIVCZWVGUYLWAUIWVHWVJVHA
        UUOVVLWVJDUYLWADAVLZVVKWVIKIWVKVVJWVBLIVVCUYLVVIWGWHWHWIXGWVJUYLJIUBUJZ
        VVEEUQZUCUJZEUQZVEZULZUCIVCZUBIVCZWVGWVIWVRKUBIKUBVLZWVBWVQLUCIIWVTVWBW
        VBVDZWVNIUIZWVQVDZLUCWVTUCUUCWWAWWAUCVIVJWVTWWAUCUUCUUPWVTLUCVLZWWAWWCV
        HWVTWWDVDZVWBWWBWVBWVQWWEVVGWVNIWVTWWDXHZXEWWEVVIWVPUYLWWEJIVVHWVOWWEVV
        HWVOULVXIWWEVVFWVMVVGWVNEWWEVVDWVLVVEEWVTWWDXBVNWWFUUQVOVPVQUURUUSUUTUV
        AWKUYOWVSWVFUYOUYNJIUDUJZVVEEUQZUEUJZEUQZVEZULZUEIVCZUDIVCZWVSWVFVJZUYO
        UYNVVMUIZUYNVVIULZLIVCZKIVCZWWNFVVMUYNPUVBUYNWAUIWWPWWSVHBUUOVVLWWSDUYN
        WADBVLZVVKWWRKIWWTVVJWWQLIVVCUYNVVIWGWHWHWIXGWWRWWMKUDIKUDVLZWWQWWLLUEI
        IWXAVWBWWQVDZWWIIUIZWWLVDZLUEWXAUEUUCWXBWXBUEVIVJWXAWXBUEUUCUUPWXALUEVL
        ZWXBWXDVHWXAWXEVDZVWBWXCWWQWWLWXFVVGWWIIWXAWXEXHXEWXFVVIWWKUYNWXFJIVVHW
        WJWXFVXIVDZVVFWWHVVGWWIEWXGVVDWWGVVEEWXAWXEVXIUVCVNWXAWXEVXIUVDUUQVPVQU
        URUUSUUTUVAWKUVEWWLWWOUDUEIIWVSWWGIUIZWXCVDZWWLWVFWVQWXIWWLWVFVJZVJUBUC
        IIWVLIUIZWWBVDZWXIWVQWXJWXLWXIWVQWXJVJWVQWWLVVPWXLWXIVDZWVEWVQWWLVVPWXM
        WVEVJZVJWVQWWLVDZVVPVVCWVPWWKUKZULZWXNWXOUYQWXPVVCWXOUYLWVPUYNWWKWVQWWL
        XBWVQWWLXHUVFVQWXQWXMUYKVVLWXQWXMUYKUMZWVLWWGEUQZIUIZWWIWVNEUQZIUIZVVCJ
        IWXSVVEEUQZWYAEUQZVEZULZVVLWXRVXMWXKWXHWXTUYKWXQVXMWXMVXNYLZWXKWWBWXIWX
        QUYKUVGWXHWXCWXLWXQUYKUVHWVLWWGEIMXIXJWXRVXMWXCWWBWYBWYGWXHWXCWXLWXQUYK
        UVIWXKWWBWXIWXQUYKUVJWWIWVNEIMXIXJWXQWXMUYKWYFWXMUYKWXQWYFWXMUYKWXQWYFV
        JWXMUYKVDZWXQWYFWYHWXPWYEVVCWYHVXMWXKWXHVDZWWBWXCVDZWXPWYEULUYKVXMWXMVX
        NWEWXMWYIUYKWXLWXKWXIWXHWXKWWBXBWXHWXCXBUVKVOWXMWYJUYKWXLWWBWXIWXCWXKWW
        BXHWXHWXCXHUVKVOVXMWYIWYJUMZWXPUFIWVLUFUJZEUQZWVNEUQZVEZWWKUKWYEWYKWVPW
        YOWWKWVPWYOULWYKJUFCIWVOWYNUYPWVOUIUFUUCUYPWYNUIJUUCJUFVLWVMWYMWVNEVVEW
        YLWVLEVRVNUVLUUPUVMJUFWVLWWGWVNWWIWWKWYOEIWYOVKWWKVKMUVNUVOXJVQUVPUUSYA
        UVQVVJWYFVVCJIWYCVVGEUQZVEZULKLWXSWYAIIVVDWXSULZVVIWYQVVCWYRJIVVHWYPWYR
        VVHWYPULVXIWYRVVFWYCVVGEVVDWXSVVEEVMVNVOVPVQVVGWYAULZWYQWYEVVCWYSJIWYPW
        YDWYSVXIVDVVGWYAWYCEWYSVXIXBUVRVPVQVSXJXTUVSUUSUVTUUSUWAUUMUWBUUMUWCUUN
        UWCUWCPYGUVQUUNUWDUWEUWFVVLVVRDUWGUWHVVRVVNDCDCVLZVVQUYSABWYTVVPUYRUYMU
        YOVVCUYPUYQWGUWIUWJUWKUWLPUYSABCUWMUWNUYKVVAVVBFFUTZFUYTVAZVUHSFVBZRFVB
        ZQFVBZVUNVUPRFVCZVDZQFVBZTFVCZUMZUYKXUBXUEXUIUYKUYQFUIBFVBAFVBXUBABDEFG
        HIJKLMNOPUWOABCFFUYQFUYTUYSWVCUYRVDABCUYMUYOUYRUUIUWPZUWQYTABCQRSDEFGHI
        JKLMNOPUWRUYKAIWUFUYLEUQZWUFEUQZVEZFUIZXUIUYKXUNVVMFUYKXUNVVIULZLIVCZKI
        VCZXUNVVMUIUYKWUGWUGXUNJIWUFVVEEUQZWUFEUQZVEZULZXURUYKVXMWUGVXNWUHWDZXV
        CXVBUYKAJUGIXUMXUTUGUJZXUMUIJUUCXVDXUTUIAUUCAJVLXULXUSWUFEUYLVVEWUFEVRV
        NUVLUUPXUPXVBXUNJIXUSVVGEUQZVEZULKLWUFWUFIIVVDWUFULZVVIXVFXUNXVGJIVVHIX
        VEXVGIXRXVGVVFXUSVVGEVVDWUFVVEEVMVNXSVQVVGWUFULZXVFXVAXUNXVHJIXVEIXUTXV
        HIXRVVGWUFXUSEVRXSVQVSXJVVLXURDXUNVXLXUNWAUIIWUNWAMEEVYGWANHWPUWSUWTUXA
        UWTZAIXUMWAWFXGVVCXUNULZVVKXUQKIXVJVVJXUPLIVVCXUNVVIWGWHWHUXBUWHPWNUYKX
        UOVDZXUNVUEUYTUQZVUEULZVUOXUNULZRFVCZVDZQFVBZXUIXVKXVMQFVBXVOQFVBZXVQAB
        CQDEFHUXCWQZHIJKLMNXVSVKPUXDUYKXVRXUOABCQRDEFGHIJKLMNOPUXEVOXVMXVOQFUXF
        UXGUYKXUHXVQTXUNFVULXUNULZXUHXVQVHUYKXVTXUGXVPQFXVTVUNXVMXUFXVOXVTVUMXV
        LVUEVULXUNVUEUYTVMUXHXVTVUPXVNRFVULXUNVUOUXIWHUURUXJWEUXKUXLUXMYQVVAXUJ
        VHVUBFVUBFULZVUDXUBVUKXUEVUTXUIVUCXUAULZXWAVUDXUBVHXWAXWBVUBFVUBFUXNUXO
        VUCVUBXUAFUYTUXPUXQXWAVUJXUDQVUBFXWAUXRZXWAVUIXUCRVUBFXWCXWAVUHSVUBFXWC
        UXSUXTUXTXWAVUSXUHTVUBFXWCXWAVURXUGQVUBFXWCXWAVUQXUFVUNXWAVUPRVUBFXWCUY
        AUYBUXTUYCUYDUYEXFYHUYKUYTWAUIZVUAVVAVHUYKFWAUIZXWEVDZXWDXWFUYKXWEXWEFV
        VMWAPKLDIIVVIXVIXVIUYFUWTZXWGUYGUUPABCFFUYQWAWAUYTXUKUYHWDQRSTWAUYTVUBV
        UBVKUYIWDWJDEFGHIJKLMNOPUYJXK $.
        $( [17-Jul-2012] $)
    $}
  $}

  $c opfn $.

  $( Extend class notation with an operator that derives an operation on
     functions from an operation on the elements of the common range of those
     functions. $)
  copfn $a class opfn $.


  ${
    $d g i h x y z a b $.
    $( Multiplication or addition of two functions ` x ` and ` y ` derived from
       the operation ` g ` on the elements of the common range of ` x ` and
       ` y ` .  The functions ` x ` and ` y ` must also have the same domain
       ` i ` . $)
    df-opfn $a |- opfn = { <. <. g , i >. , h >. |
       h = { <. <. x , y >. , z >. |
         ( x e. ( dom dom g ^m i ) /\ y e. ( dom dom g ^m i ) /\
       z = { <. a , b >. | ( a e. dom x /\ b = ( ( x ` a ) g ( y ` a ) ) ) } )
        }
        } $.
  $}

