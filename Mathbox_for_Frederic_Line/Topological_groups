$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topological groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TopGrp $.

  $( Extend class notation with the class of all topological groups. $)
  ctopgrp $a class TopGrp $.

  ${
    $d g j $.
    $( Define the class of all topological groups.  A topological group is a
       group whose operation and inverse function are continuous. $)
    df-topgrp $a |- TopGrp = { <. g , j >. | ( ( g e. GrpOp /\ j e. Top ) /\
         g e. ( ( j tX j ) Cn j ) /\ ( inv ` g ) e. ( j Cn j ) ) } $.
  $}

  ${
    $d G g j $.  $d J g j $.  $d x y $.
    $( The predicate "is a topological group".  Bourbaki TG III.1 (Contributed
       by FL, 18-Apr-2010.) $)
    istopgrp $p |- ( J e. A -> ( <. G , J >. e. TopGrp <->
      ( ( G e. GrpOp /\ J e. Top ) /\ G e. ( ( J tX J ) Cn J )
        /\ ( inv ` G ) e. ( J Cn J ) ) ) ) $=
      ( vx vy vg vj wcel ctopgrp cgr ctop wa ctx co ccn cgn cfv w3a cv eleq1 id
      cop cvv wbr df-br wrel copab relopab df-topgrp releqi mpbir sylbir adantl
      brrelexi simpl jca elex adantr 3ad2ant1 eleq2i anbi1d fveq2 eleq1d anbi2d
      wceq 3anbi123d oveq12d eleq2d opelopabg syl5bb pm5.21nd ) CAHZBCUBZIHZBJH
      ZCKHZLZBCCMNZCONZHZBPQZCCONZHZRZBUCHZVLLZVLVNLWEVLVNWEVLVNBCIUDWEBCIUEBCI
      IUFDSZJHESZKHLWGWHWHMNWHONHWGPQWHWHONHRZDEUGZUFWIDEUHIWJDEUIUJUKUNULUMVLV
      NUOUPVLWDLWEVLWDWEVLVQVTWEWCVOWEVPBJUQURUSUMVLWDUOUPVNVMFSZJHZGSZKHZLZWKW
      MWMMNZWMONZHZWKPQZWMWMONZHZRZFGUGZHWFWDIXCVMFGUIUTXBVOWNLZBWQHZWAWTHZRWDF
      GBCUCAWKBVEZWOXDWRXEXAXFXGWLVOWNWKBJTVAWKBWQTXGWSWAWTWKBPVBVCVFWMCVEZXDVQ
      XEVTXFWCXHWNVPVOWMCKTVDXHWQVSBXHWPVRWMCOXHWMCWMCMXHUAZXIVGXIVGVHXHWTWBWAX
      HWMCWMCOXIXIVGVHVFVIVJVK $.
      $( [18-Apr-2010] $)
  $}

  ${
    topgrpi.1 $e |- G = ( 1st ` K ) $.
    ${
      topgrpi.2 $e |- J = ( 2nd ` K ) $.
      ${
        $d G g j $.  $d J g j $.  $d K g j $.  $d N g j $.
        topgrpi.3 $e |- N = ( inv ` G ) $.
        $( "Axioms" of a topological group.  (Contributed by FL,
           18-Apr-2010.) $)
        topgrpi $p |- ( K e. TopGrp -> ( ( G e. GrpOp /\ J e. Top ) /\
          G e. ( ( J tX J ) Cn J ) /\ N e. ( J Cn J ) ) ) $=
          ( vg vj wcel cgr ctop wa ctx co ccn cgn cfv w3a wceq ctopgrp cv copab
          df-topgrp eleq2i wb eqtr3 eqcomd eleq1 anbi1d fveq2 syl6eqr 3anbi123d
          c1st eleq1d syl mpan c2nd anbi2d oveq12 anidms mpancom eleq2d elopabi
          sylbi ) CUAJCHUBZKJZIUBZLJZMZVFVHVHNOZVHPOZJZVFQRZVHVHPOZJZSZHIUCZJAK
          JZBLJZMZABBNOZBPOZJZDBBPOZJZSZUAVRCHIUDUEVQVSVIMZAVLJZDVOJZSZWGHICACU
          NRZTZVFWLTZVQWKUFZEWMWNMZVFATZWOWPAVFAVFWLUGUHWQVJWHVMWIVPWJWQVGVSVIV
          FAKUIUJVFAVLUIWQVNDVOWQVNAQRDVFAQUKGULUOUMUPUQBCURRZTZVHWRTZWKWGUFZFW
          SWTMZVHBTZXAXBBVHBVHWRUGUHXCWHWAWIWDWJWFXCVIVTVSVHBLUIUSXCVLWCAVKWBTZ
          XCVLWCTXCXDVHBVHBNUTVAVKWBVHBPUTVBVCXCVOWEDXCVOWETVHBVHBPUTVAVCUMUPUQ
          VDVE $.
          $( [18-Apr-2010] $)

        $( In a topological group, the inverse function is continuous.
           (Contributed by FL, 21-Jun-2010.) $)
        topgrpinv $p |- ( K e. TopGrp -> N e. ( J Cn J ) ) $=
          ( ctopgrp wcel cgr ctop wa ctx co ccn topgrpi simp3d ) CHIAJIBKILABBM
          NBONIDBBONIABCDEFGPQ $.
          $( [21-Jun-2010] $)
      $}

      $( In a topological group, the base set of the topology and the base set
         of the group are equal.  (Contributed by FL, 21-Jun-2010.) $)
      topgrpbs $p |- ( K e. TopGrp -> ran G = U. J ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn w3a wceq eqid wi wf fdm syl eqtr2
        cgn cfv crn cuni topgrpi wf1o grpoinvf cnf f1of cdm syl2im syl5com 3exp
        ex pm2.43i com3r imp31 3adant2 ) CFGAHGZBIGZJZABBKLBMLGZAUBUCZBBMLGZNAU
        DZBUEZOZABCVDDEVDPZUFVBVEVHVCUTVAVEVHUTVFVFVDUGZVAVEVHQQAVDVFVFPVIUHVAV
        EVJVHVAVEVJVHQZQVAVAVEVKVAVAVENVGVGVDRZVJVHVDBBVGVGVGPZVMUIVJVFVFVDRZVL
        VHQVFVFVDUJVNVDUKZVFOZVLVOVGOZVHVFVFVDSVGVGVDSVPVQVHVOVFVGUAUOULTUMUNUP
        UQTURUST $.
        $( [21-Jun-2010] $)

      $( In a topological group, the operation ` G ` is continuous.
         (Contributed by FL, 21-Jun-2010.) $)
      topgrpcn $p |- ( K e. TopGrp -> G e. ( ( J tX J ) Cn J ) ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv eqid topgrpi simp2d ) CFG
        AHGBIGJABBKLBMLGANOZBBMLGABCSDESPQR $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the operation ` G ` is a group.  (Contributed by
       FL, 21-Jun-2010.) $)
    topgrpgrp $p |- ( K e. TopGrp -> G e. GrpOp ) $=
      ( ctopgrp wcel cgr c2nd cfv ctop wa ctx co ccn eqid topgrpi simp1d simpld
      cgn ) BDEZAFEZBGHZIEZSTUBJAUAUAKLUAMLEARHZUAUAMLEAUABUCCUANUCNOPQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrptop.1 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, ` J ` is a topology.  (Contributed by FL,
       21-Jun-2010.) $)
    topgrptop $p |- ( K e. TopGrp -> J e. Top ) $=
      ( ctopgrp wcel c1st cfv cgr ctop wa ctx co ccn eqid topgrpi simp1d simprd
      cgn ) BDEZBFGZHEZAIEZSUAUBJTAAKLAMLETRGZAAMLETABUCTNCUCNOPQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    extopgrp.1 $e |- A e. _V $.
    $( The trivial topological group.  (Contributed by FL, 21-Jun-2010.)
       (Revised by Mario Carneiro, 15-Dec-2013.) $)
    extopgrp $p |- <. { <. <. A , A >. , A >. } , { (/) , { A } } >.
       e. TopGrp $=
      ( cop csn c0 cpr ctopgrp wcel cgr wa ctx co ccn cgn cfv cvv w3a ax-mp cun
      ctop wb prex istopgrp elexi grposn indistop pm3.2i cxp wf txtopi wfo opex
      crn rnsnop eqcomi grpofo fof syl cuni 0ex snex unipr uncom 3eqtrri txunii
      un0 mapudiscn mp2an cid cres ginvsn idcn eqeltri mpbir3an ) AACZACDZEADZF
      ZCGHZVPIHZVRTHZJZVPVRVRKLZVRMLHZVPNOZVRVRMLZHZVRPHVSWBWDWGQUAEVQUBPVPVRUC
      RVTWAAAPBUDZUEZVQUFZUGWCTHVQVQUHZVQVPUIZWDVRVRWJWJUJVTWLWIVTWKVQVPUKWLVPV
      QVPUMVQVOAAAULWHUNUOUPWKVQVPUQURRWKVQVPWCVRVRVQVQWJWJVRUSEVQSVQESVQEVQUTA
      VAZVBEVQVCVQVFVDZWNVEWMVGVHWEVIVQVJZWFABVKWAWOWFHWJVRVQWNVLRVMVN $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    $d x y z G $.  $d x y z J $.  $d x y z K $.
    topgrpsubcn.1 $e |- G = ( 1st ` K ) $.
    topgrpsubcn.2 $e |- J = ( 2nd ` K ) $.
    topgrpsubcn.3 $e |- D = ( /g ` G ) $.
    $( In a topological group, the "subtraction" (or "division") is
       continuous.  Bourbaki TG III.1 axiom GT' (Contributed by FL,
       21-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       30-May-2014.) $)
    topgrpsubcn $p |- ( K e. TopGrp -> D e. ( ( J tX J ) Cn J ) ) $=
      ( vx vy ctopgrp wcel cuni cv cgn cfv co cmpt2 ctx wceq eqid ccn topgrpgrp
      crn cgr grpodivfval syl topgrpbs mpt2eq12 syl2anc eqtrd cnmpt1st cnmpt2nd
      topgrptop topgrpinv cnmpt21f topgrpcn cnmpt22f eqeltrd ) DJKZAHICLZUTHMZI
      MZBNOZOZBPZQZCCRPCUAPUSAHIBUCZVGVEQZVFUSBUDKAVHSBDEUBHIABVCVGVGTVCTZGUEUF
      USVGUTSZVJVHVFSBCDEFUGZVKHIVGVGUTUTVEUHUIUJUSHIVAVDBCCCCCUTUTUTUTUTTZVLCD
      FUMZVMVMVMUSHICCUTUTVLVLVMVMUKUSHIVBVCCCCCUTUTVLVLVMVMVMVMUSHICCUTUTVLVLV
      MVMULBCDVCEFVIUNUOVLVLVMBCDEFUPUQUR $.
      $( [23-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    $d x y A $.  $d y F $.  $d x y G $.  $d x y J $.  $d x y K $.  $d x y X $.
    $d y O $.
    trhom.1 $e |- X = ran G $.
    trhom.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trhom.3 $e |- G = ( 1st ` K ) $.
    trhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group a right translation is a homeomorphism.  Bourbaki
       TG III.2 (Contributed by FL, 21-Jun-2010.)  (The proof was shortened by
       Mario Carneiro, 30-May-2014.) $)
    trhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
      ( wcel wa co ccn adantr cmpt wceq syl5eq mpteq1 ctopgrp ccnv wb topgrptop
      chomeosm ctop hmeobc syl2anc cuni crn topgrpbs eqid cnmptid simpr eleqtrd
      cv syl cnmptc ctx topgrpcn cnmpt12f eqeltrd cgs cfv topgrpgrp trinv sylan
      cgr eqtrd topgrpsubcn mpbir2and ) FUALZBGLZMZCEEUENLZCEEONZLZCUBZVPLZVNEU
      FLZVTVOVQVSMUCVLVTVMEFKUDPZWACEEUGUHVNCAEUIZAUPZBDNZQZVPVNCAGWDQZWEIVNGWB
      RZWFWERVNGDUJZWBHVLWHWBRVMDEFJKUKPSZAGWBWDTUQSVNAWCBDEEEEWBWBWBULZWAWJWAW
      AVNAEWBWJWAUMZVNABEEWBWBWJWAWJWAVNBGWBVLVMUNWIUOURZWAVLDEEUSNEONZLVMDEFJK
      UTPVAVBVNVRAWBWCBDVCVDZNZQZVPVNVRAGWOQZWPVLDVHLVMVRWQRDFJVEABWNCDGIHWNULZ
      VFVGVNWGWQWPRWIAGWBWOTUQVIVNAWCBWNEEEEWBWBWJWAWJWAWAWKWLWAVLWNWMLVMWNDEFJ
      KWRVJPVAVBVK $.
      $( [21-Jun-2010] $)
    $( In a topological group, a converse of a right translation is a
       homeomorphism.  Bourbaki TG III.2 (Contributed by FL, 30-Dec-2010.) $)
    cnvtrhom $p |- ( ( K e. TopGrp /\ A e. X )
        -> `' F e. ( J Homeo J ) ) $=
      ( ctopgrp wcel wa ctop chomeosm co ccnv topgrptop jca trhom cnvhmpha sylc
      adantr ) FLMZBGMZNEOMZUGNZCEEPQZMCRUIMUEUHUFUEUGUGEFKSZUJTUDABCDEFGHIJKUA
      CEEUBUC $.
      $( [30-Dec-2010] $)
    $( In a topological group the image of an open set by a right translation
       is an open set.  Bourbaki TG III.2 (Contributed by FL, 30-Dec-2010.) $)
    trhom2 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
       -> ( F " O ) e. J ) $=
      ( vy ctopgrp wcel cima chomeosm co wa wi trhom cv wral ctop topgrptop jca
      adantr hmeocnb syl wceq imaeq2 eleq1d rcla4cv syl6com mpcom 3impia ) FNOZ
      BHOZGEOZCGPZEOZCEEQROZUQURSZUSVATZABCDEFHIJKLUAVCVBCMUBZPZEOZMEUCZVDVCEUD
      OZVISZVBVHTUQVJURUQVIVIEFLUEZVKUFUGMCEEUHUIVGVAMGEVEGUJVFUTEVEGCUKULUMUNU
      OUP $.
      $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    tpgprop.5 $e |- .t = ( cset ` G ) $.
    $( In a topological group, the product of an open set by an element is
       open .  (Contributed by FL, 30-Dec-2010.) $)
    tpgprop1 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
        -> ( O .t { A } ) e. J ) $=
      ( ctopgrp wcel w3a cpw wceq wss csn cima cgr topgrpgrp 3ad2ant1 simp2 crn
      co wi cuni topgrpbs eqtr ex pwuni pweq sseq2d mpbiri syl6 mpsyl sseld a1d
      3imp prsubrtr2 syl3anc trhom2 eqeltrd ) FOPZBHPZGEPZQZGBUAIUHZCGUBZEVJDUC
      PZVHGHRZPZVKVLSVGVHVMVIDFLUDUEVGVHVIUFVGVHVIVOVGVIVOUIVHVGEVNGHDUGZSZVGVP
      EUJZSZEVNTZJDEFLMUKVQVSHVRSZVTVQVSWAHVPVRULUMWAVTEVRRZTEUNWAVNWBEHVRUOUPU
      QURUSUTVAVBABGCDIHKJNVCVDABCDEFGHJKLMVEVF $.
      $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $( In a topological group, the product of an open set by another set is
       open .  (Contributed by FL, 1-Jan-2011.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    tpgprop2 $p |- ( ( K e. TopGrp /\ A e. ~P X /\ O e. J )
        -> ( O .t A ) e. J ) $=
      ( vy wcel cpw co wceq 3ad2ant1 ctopgrp w3a csn ciun cgr cdm topgrpgrp wss
      cuni pwuni a1i crn topgrpbs syl5eq pweq syl sseqtr4d ssel2 sylan grporndm
      cv 3adant2 syl5req eleqtrrd simp2 eqid iscst4 syl3anc ctop wral topgrptop
      wa simpl1 wi elelpwi expcom 3ad2ant2 imp simpl3 tpgprop1 ralrimiva iunopn
      cmpt syl2anc eqeltrd ) FUAPZBHQZPZGEPZUBZGBIRZOBGOVAZUCIRZUDZEWJDUEPZGDUF
      UFZQZPBWQPWKWNSWFWHWOWIDFLUGZTWJGWGWQWFWIGWGPZWHWFEWGUHWIWSWFEEUIZQZWGEXA
      UHWFEUJUKWFHWTSWGXASWFHDULZWTJDEFLMUMUNHWTUOUPUQEWGGURUSVBWJWPHSZWQWGSWFW
      HXCWIWFWOXCWRWOHXBWPJDUTVCUPTWPHUOUPZVDWJBWGWQWFWHWIVEXDVDOGBDIUEWPWPVFNV
      GVHWJEVIPZWMEPZOBVJWNEPWFWHXEWIEFMVKTWJXFOBWJWLBPZVLWFWLHPZWIXFWFWHWIXGVM
      WJXGXHWHWFXGXHVNWIXGWHXHWLBHVOVPVQVRWFWHWIXGVSAWLAHAVAWLDRWCZDEFGHIJXIVFL
      MNVTVHWAOBWMEWBWDWE $.
      $( [15-Dec-2013] $) $( [1-Jan-2011] $)
  $}

  ${
    $d x A $.  $d x G $.  $d x J $.  $d x K $.  $d x X $.
    ltrhom.1 $e |- X = ran G $.
    ltrhom.2 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrhom.3 $e |- G = ( 1st ` K ) $.
    ltrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a left translation is a homeomorphism. ` A ` is
       constant.  (Contributed by FL, 12-Jul-2012.)  (The proof was shortened
       by Mario Carneiro, 31-May-2014.) $)
    ltrhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
      ( wcel wa co ccn adantr cmpt wceq eqid cfv ctopgrp ccnv ctop wb topgrptop
      chomeosm hmeobc syl2anc cuni crn topgrpbs syl5eq mpteq1 syl simpr eleqtrd
      cv cnmptc cnmptid ctx topgrpcn cnmpt12f eqeltrd topgrpgrp ltrinvlem sylan
      cgn cgr cgs eqtrd grpoinvcl mpbir2and ) FUALZBGLZMZCEEUFNLZCEEONZLZCUBZVQ
      LZVOEUCLZWAVPVRVTMUDVMWAVNEFKUEPZWBCEEUGUHVOCAEUIZBAUQZDNZQZVQVOCAGWEQZWF
      IVOGWCRZWGWFRVOGDUJZWCHVMWIWCRVNDEFJKUKPULZAGWCWEUMUNULVOABWDDEEEEWCWCWCS
      ZWBWKWBWBVOABEEWCWCWKWBWKWBVOBGWCVMVNUOWJUPURVOAEWCWKWBUSZWBVMDEEUTNEONLV
      NDEFJKVAPZVBVCVOVSAWCBDVGTZTZWDDNZQZVQVOVSAGWPQZWQVMDVHLZVNVSWRRDFJVDZABD
      VITZCDWNGIHXASWNSZVEVFVOWHWRWQRWJAGWCWPUMUNVJVOAWOWDDEEEEWCWCWKWBWKWBWBVO
      AWOEEWCWCWKWBWKWBVOWOGWCVMWSVNWOGLWTBDWNGHXBVKVFWJUPURWLWBWMVBVCVL $.
      $( [31-May-2014] $) $( [12-Jul-2012] $)
  $}


  ${
    $d x y A $.  $d x y B $.  $d x y G $.  $d x y J $.  $d x y K $.
    $d x y X $.
    lrtrhom.1 $e |- X = ran G $.
    lrtrhom.2 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    lrtrhom.3 $e |- G = ( 1st ` K ) $.
    lrtrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a right and left translation is a
       homeomorphism.  Note that ` A ` and ` B ` are constant.  (Contributed by
       FL, 12-Jul-2012.) $)
    lrtrhom $p |- ( ( K e. TopGrp /\ A e. X /\ B e. X )
       -> F e. ( J Homeo J ) ) $=
      ( vy ctopgrp wcel w3a cv co cmpt eqid ccom chomeosm cgr topgrpgrp syl3an1
      wceq cmperltr ctop wa topgrptop 3ad2ant1 trhom 3adant2 ltrhom 3adant3 jca
      3jca cmphmp sylc eqeltrd ) GNOZBHOZCHOZPZDMHBMQERSZAHAQCERSZUAZFFUBRZVAEU
      COVBVCDVGUFEGKUDAMBCVEDEVFHJVETZVFTZIUGUEVDFUHOZVKVKPZVFVHOZVEVHOZUIVGVHO
      VAVBVLVCVAVKVKVKFGLUJZVOVOUQUKVDVMVNVAVCVMVBACVFEFGHIVJKLULUMVAVBVNVCMBVE
      EFGHIVIKLUNUOUPVFVEFFFURUSUT $.
      $( [12-Jul-2012] $)
  $}

  ${
    $d A o u v x $.  $d B o u v x $.  $d G a b o u v $.  $d X a b o u v x $.
    grphmlem0.1 $e |- H = { o | E. a e. X E. b e. X
         o = ( x e. X |-> ( ( a G x ) G b ) ) } $.
    $( Condition for a left and right translation to belong to the class of all
       the left and right translations over the class ` X ` .  (Contributed by
       FL, 16-Jul-2012.) $)
    grphmlem0 $p |- ( ( A e. X /\ B e. X /\ X e. C )
       -> ( x e. X |-> ( ( A G x ) G B ) ) e. H ) $=
      ( vu vv wcel cv co cmpt wceq wrex adantr w3a cab eqid oveq1 oveq1d eqeq2d
      mpteq2dva oveq2 rcla42ev mp3an3 3adant3 wb mptexg 3ad2ant3 eqeq1 rexbidva
      cvv elabg syl mpbird weq wa simpl rexbidv oveq2d cbvrexv syl6bb syl6eleqr
      abbii ) BHNZCHNZHDNZUAZAHBAOZFPZCFPZQZEOZAHIOZVNFPZJOZFPZQZRZJHSZIHSZEUBZ
      GVMVQVRAHLOZVNFPZMOZFPZQZRZMHSZLHSZEUBZWGVMVQWPNZVQWLRZMHSZLHSZVJVKWTVLVJ
      VKVQVQRZWTVQUCWRXAVQAHVOWJFPZQZRLMBCHHWHBRZWLXCVQXDAHWKXBXDWKXBRVNHNZXDWI
      VOWJFWHBVNFUDUETUGUFWJCRZXCVQVQXFAHXBVPXFXBVPRXEWJCVOFUHTUGUFUIUJUKVMVQUQ
      NZWQWTULVLVJXGVKAHVPDUMUNWOWTEVQUQVRVQRZWNWSLHXHWNWSULWHHNXHWMWRMHXHWMWRU
      LWJHNVRVQWLUOTUPTUPURUSUTWFWOEWEWNILHILVAZWEVRAHWIWAFPZQZRZJHSWNXIWDXLJHX
      IWCXKVRXIAHWBXJXIXEVBZVTWIWAFXMVSWHVNFXIXEVCUEUEUGUFVDXLWMJMHJMVAZXKWLVRX
      NAHXJWKXNXEVBWAWJWIFXNXEVCVEUGUFVFVGVFVIVHKVH $.
      $( [16-Jul-2012] $)
  $}

  ${
    grphm.1 $e |- X = ran G $.
    grphm.2 $e |- G = ( 1st ` K ) $.
    grphm.3 $e |- J = ( 2nd ` K ) $.
    grphm.4 $e |- H = { o | E. a e. X E. b e. X
         o = ( p e. X |-> ( ( a G p ) G b ) ) } $.

    ${
      $d G a b c d e f o p $.  $d G c d e f p q $.  $d G c d p q z $.
      $d H c d e f y $.  $d K c d e f x y $.  $d X a b c d e f o p $.
      $d X c d e f p q $.  $d o x y $.
      $( Lemma for ~ grpohm .  Closure. $)
      grpohmlem1 $p |- ( K e. TopGrp
       -> A. x e. H
             A. y e. H ( x o. y ) e. H ) $=
        ( wcel cv wa co wceq vc vd ve vf vq vz ctopgrp ccom cmpt wrex wi eleq2i
        anbi12i oveq1 oveq1d adantr mpteq2dva eqeq2d oveq2 cbvrex2v abbii simpl
        cab weq oveq2d bitri ax-17 vex eqeq1d 2rexbidva elabf cbvmpt eqeq2i w3a
        wb eqeq1 topgrpgrp 3ad2ant3 anim12i ancoms 3adant3 eqid cmprltr syl3anc
        cgr simpr simp2l simp1l grpocl simp1r simp2r eqidd rcla42ev cvv crn cfv
        c1st fvex eqeltri rnex mptexg ax-mp elab3 sylibr syl6eleqr eqeltrd 3exp
        a1i coeq12d eleq1d imbi2d syl5ibr sylbi com4l com12 rexlimivv com3l imp
        ex syl2anb ralrimivv ) GUGPZAQZBQZUHZEPZABEEYCEPZYDEPZRZYBYFYIYCCQZIHUA
        QZIQZDSZUBQZDSZUIZTZUBHUJUAHUJZCVCZPZYDYJIHUCQZYLDSZUDQZDSZUIZTZUDHUJUC
        HUJZCVCZPZRZYBYFUKZYIYCYJIHJQZYLDSZKQZDSZUIZTZKHUJJHUJZCVCZPZYDUUSPZRUU
        JYGUUTYHUVAEUUSYCOULEUUSYDOULUMUUTYTUVAUUIUUSYSYCUURYRCUUQYQYJIHYMUUNDS
        ZUIZTJKUAUBHHJUAVDZUUPUVCYJUVDIHUUOUVBUVDUUOUVBTYLHPZUVDUUMYMUUNDUULYKY
        LDUNUOUPUQURKUBVDZUVCYPYJUVFIHUVBYOUVFUVBYOTUVEUUNYNYMDUSUPUQURUTVAULUU
        SUUHYDUURUUGCUUQUUFYJIHUUBUUNDSZUIZTJKUCUDHHJUCVDZUUPUVHYJUVIIHUUOUVGUV
        IUVERUUMUUBUUNDUVIUUMUUBTUVEUULUUAYLDUNUPUOUQURKUDVDZUVHUUEYJUVJIHUVGUU
        DUVJUVERUUNUUCUUBDUVJUVEVBVEUQURUTVAULUMVFYTYCYPTZUBHUJUAHUJZYDUUETZUDH
        UJUCHUJZUUKUUIYRUVLCYCUVLCVGAVHCAVDZYQUVKUAUBHHUVOYKHPZYNHPZRZRYJYCYPUV
        OUVRVBVIVJVKUUGUVNCYDUVNCVGBVHCBVDZUUFUVMUCUDHHUVSUUFUVMVOUUAHPZUUCHPZR
        ZYJYDUUEVPUPVJVKUVLUVNUUKUVKUVNUUKUKUAUBHHUVNUVRUVKUUKUVMUVRUVKUUKUKUKZ
        UCUDHHUVMUWBUWCUVKUVMUWBUVRUUKUVKYCUEHYKUEQZDSZYNDSZUIZTZUVMUWBUVRUUKUK
        ZUKZUKYPUWGYCIUEUFHYOUWFUFQZYOPUEVGUWKUWFPIVGIUEVDYMUWEYNDYLUWDYKDUSUOV
        LVMUWHUVMUWJUWBUWIUWHUVMRZUVRYBUWGUUEUHZEPZUKZUKUWBUVRYBUWNUWBUVRYBVNZU
        WMIHYKUUADSZYLDSZUUCYNDSZDSZUIZEUWPDWEPZUVPUVTRZUVQUWARZUWMUXATYBUWBUXB
        UVRDGMVQVRZUWBUVRUXCYBUVRUWBUXCUVRUVPUWBUVTUVPUVQVBUVTUWAVBVSVTWAUWBUVR
        UXDYBUVRUWBUXDUVRUVQUWBUWAUVPUVQWFUVTUWAWFVSVTWAIUEYKUUAYNUUCUUEUWGDHUW
        GWBUUEWBLWCWDUWPUXAUUSEUWPUXAUUPTZKHUJJHUJZUXAUUSPUWPUWQHPZUWSHPZUXAUXA
        TZUXGUWPUXBUVPUVTUXHUXEUWBUVPUVQYBWGUVTUWAUVRYBWHYKUUADHLWIWDUWPUXBUWAU
        VQUXIUXEUVTUWAUVRYBWJUWBUVPUVQYBWKUUCYNDHLWIWDUWPUXAWLUXFUXJUXAIHUWRUUN
        DSZUIZTJKUWQUWSHHUULUWQTZUUPUXLUXAUXMIHUUOUXKUXMUUOUXKTUVEUXMUUMUWRUUND
        UULUWQYLDUNUOUPUQURUUNUWSTZUXLUXAUXAUXNIHUXKUWTUXNUVERUUNUWSUWRDUXNUVEV
        BVEUQURWMWDUURUXGCUXAUXAWNPZUXGHWNPUXOHDWOWNLDDGWQWPWNMGWQWRWSWTWSIHUWT
        WNXAXBXHYJUXATZUUQUXFJKHHUXPUULHPUUNHPRZRYJUXAUUPUXPUXQVBVIVJXCXDOXEXFX
        GUWLUUKUWOUVRUWLYFUWNYBUWLYEUWMEUWLYCUWGYDUUEUWHUVMVBUWHUVMWFXIXJXKXKXL
        XSXMXNXOXPXQXPXRXTXMXOYA $.
        $( [17-Jul-2012] $) $( [26-Jun-2012] $)
    $}

    ${
      $d G a b c d e i p o $.  $d H g h x y z $.  $d K a b c d e f g h i p $.
      $d X a b c d e i p o $.  $d f g h o x y z $.
      $( Lemma for ~ grpohm .  Associativity . $)
      grpohmlem2 $p |- ( K e. TopGrp -> A. f e. H A. g e. H A. h e. H (
      ( f { <. <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x
      o. y ) ) } g ) { <. <. x , y >. , z >. | ( x e. H /\ y e. H
      /\ z = ( x o. y ) ) } h ) = ( f { <. <. x , y >. , z >. | ( x
      e. H /\ y e. H /\ z = ( x o. y ) ) } ( g { <. <. x , y >. , z
      >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) } h ) ) ) $=
        ( co vc vd ve vi ctopgrp wcel cv ccom wceq w3a copab2 wral wa cmpt wrex
        wi cab vex weq simpl eqeq1d 2rexbidva eleq2i eqeq1 rexbidv oveq1 oveq1d
        elab adantr mpteq2dva eqeq2d oveq2 cbvrex2v cgr topgrpgrp anim12i simpr
        adantl 3jca cmprltr2 syl coeq1d ad2antlr simplll simplrl grpocl syl3anc
        eqid simplrr simpllr simprr syl112anc simprl coeq2d grpoass syl2anc jca
        syl122anc eqcomd oveq12d 3eqtrd coeq2 coeq1 eqeq12d syl5ibr imp expdimp
        3eqtr4rd exp3a exp4c ex com14 3imp impcom com12 rexlimivv com3l syl2anb
        3exp sylbi eleq2s ralrimiva ralrimivva ax-17 cvv grpohmlem1 ra42 df-3an
        coex a1i oprabbii ov2g imp31 ralbida 2ralbida mpbird coexg oveqi syl5eq
        oveq2d ) KUEUFZDUGZEUGZAUGZIUFZBUGZIUFZCUGUUDUUFUHZUIZUJZABCUKZTZFUGZUU
        KTZUUBUUCUUMUUKTZUUKTZUIZFIULZEIULDIULUUBUUCUHZUUMUUKTZUUBUUCUUMUHZUUKT
        ZUIZFIULZEIULDIULZUUAUVEUUSUUMUHZUUBUVAUHZUIZFIULZEIULDIULUUAUVIDEIIUUA
        UUBIUFZUUCIUFZUMZUMZUVHFIUUMIUFZUVMUVHUVMUVHUPZUUMGUGZMLNUGZMUGZHTZOUGZ
        HTZUNZUIZOLUOZNLUOZGUQZIUUMUWFUFUUMUWBUIZOLUONLUOZUVOUWEUWHGUUMFURZGFUS
        ZUWCUWGNOLLUWJUVQLUFZUVTLUFZUMZUMUVPUUMUWBUWJUWMUTVAVBVHUVMUWHUVHUVLUUA
        UWHUVHUPZUVJUUBUWFUFZUUCUWFUFZUUAUWNUPZUVKIUWFUUBSVCIUWFUUCSVCUWOUUBUWB
        UIZOLUOZNLUOZUUCUWBUIZOLUOZNLUOZUWQUWPUWEUWTGUUBDURZGDUSZUWDUWSNLUXEUWC
        UWROLUVPUUBUWBVDVEVEVHUWEUXCGUUCEURZGEUSZUWDUXBNLUXGUWCUXAOLUVPUUCUWBVD
        VEVEVHUWTUUBMLUAUGZUVRHTZUBUGZHTZUNZUIZUBLUOUALUOZUUCMLUCUGZUVRHTZUDUGZ
        HTZUNZUIZUDLUOUCLUOZUWQUXCUWRUXMUUBMLUXIUVTHTZUNZUINOUAUBLLNUAUSZUWBUYC
        UUBUYDMLUWAUYBUYDUWAUYBUIUVRLUFZUYDUVSUXIUVTHUVQUXHUVRHVFVGVIVJVKOUBUSZ
        UYCUXLUUBUYFMLUYBUXKUYFUYBUXKUIUYEUVTUXJUXIHVLVIVJVKVMUXAUXTUUCMLUXPUVT
        HTZUNZUINOUCUDLLNUCUSZUWBUYHUUCUYIMLUWAUYGUYIUWAUYGUIUYEUYIUVSUXPUVTHUV
        QUXOUVRHVFVGVIVJVKOUDUSZUYHUXSUUCUYJMLUYGUXRUYJUYGUXRUIUYEUVTUXQUXPHVLV
        IVJVKVMUXNUYAUWQUXMUYAUWQUPUAUBLLUYAUXHLUFZUXJLUFZUMZUXMUWQUXTUYMUXMUWQ
        UPZUPUCUDLLUXOLUFZUXQLUFZUMZUXTUYMUYNUYQUXTUYMUJZUXMUUAUWNUWHUYRUXMUUAU
        JZUVHUWGUYSUVHUPZNOLLUWGUWMUYTUWGUWMUYSUVHUWMUYSUMUVHUWGUUSUWBUHZUUBUUC
        UWBUHZUHZUIZUYSUWMVUDUYRUXMUUAUWMVUDUPZUYQUXTUYMUXMUUAVUEUPZUPUXMUXTUYM
        UYQVUFUXMUXTUYMUYQVUFUPUPUXMUXTUMZUYMUYQUUAVUEVUGUYMUYQUMZUUAUMZUWMVUDU
        XMUXTVUIUWMUMZVUDUXTVUJUMVUDUXMUXLUUCUHZUWBUHZUXLVUBUHZUIZUXTVUJVUNVUJV
        UNUXTUXLUXSUHZUWBUHZUXLUXSUWBUHZUHZUIVUJVUPMLUXHUXOHTZUVRHTUXQUXJHTZHTU
        NZUWBUHZMLVUSUVQHTZUVRHTZUVTVUTHTZHTZUNZVURVUJVUOVVAUWBVUJHVNUFZUYKUYOU
        MZUYLUYPUMZUJZVUOVVAUIVUIVVKUWMVUIVVHVVIVVJUUAVVHVUHHKQVOZVRZVUHVVIUUAU
        YMUYKUYQUYOUYKUYLUTUYOUYPUTVPVIVUHVVJUUAUYMUYLUYQUYPUYKUYLVQUYOUYPVQVPV
        IVSVIMMUXHUXOUXJUXQUXSUXLHLUXLWHZUXSWHZPVTWAWBVUJVVHVUSLUFZUWKUMVUTLUFZ
        UWLVVBVVGUIUUAVVHVUHUWMVVLWCZVUIVVPUWMUWKVUIVVHUYKUYOVVPVVMUYKUYLUYQUUA
        WDZUYMUYOUYPUUAWEZUXHUXOHLPWFWGUWKUWLUTVPVUJVVHUYPUYLVVQVVRVUIUYPUWMUYM
        UYOUYPUUAWIVIZVUIUYLUWMUYKUYLUYQUUAWJVIZUXQUXJHLPWFWGVUIUWKUWLWKZMMVUSU
        VQVUTUVTUWBVVAHLVVAWHUWBWHZPVTWLVUJUXLMLUXOUVQHTZUVRHTUVTUXQHTZHTUNZUHZ
        MLUXHVWEHTZUVRHTZVWFUXJHTZHTZUNZVURVVGVUJVVHUYKVWELUFZUYLVWFLUFZVWHVWMU
        IVVRVUIUYKUWMVVSVIZVUJVVHUYOUWKVWNVVRVUIUYOUWMVVTVIZVUIUWKUWLWMZUXOUVQH
        LPWFWGVWBVUJVVHUWLUYPVWOVVRVWCVWAUVTUXQHLPWFWGMMUXHVWEUXJVWFVWGUXLHLVVN
        VWGWHPVTWRVUJVUQVWGUXLVUJVVHUYOUWKUYPUWLVUQVWGUIVVRVWQVWRVWAVWCMMUXOUVQ
        UXQUVTUWBUXSHLVVOVWDPVTWRWNVUJMLVVFVWLVUJUYEUMZVVDVWJVVEVWKHVWSVVCVWIUV
        RHVWSVVHUYKUYOUWKUJZVVCVWIUIVUJVVHUYEVVRVIVUJVWTUYEVUJUYKUYOUWKVWPVWQVW
        RVSVIUXHUXOUVQHLPWOWPVGVWSVVHUWLUYPUYLUJZUMZVVEVWKUIVUJVXBUYEVUJVVHVXAV
        VRVUJUWLUYPUYLVWCVWAVWBVSWQVIVXBVWKVVEUVTUXQUXJHLPWOWSWAWTVJXHXAUXTVULV
        UPVUMVURUXTVUKVUOUWBUUCUXSUXLXBWBUXTVUBVUQUXLUUCUXSUWBXCWNXDXEXFUXMVUAV
        ULVUCVUMUXMUUSVUKUWBUUBUXLUUCXCWBUUBUXLVUBXCXDXEXGXIXJXKXLXMXMXNUWGUVFV
        UAUVGVUCUUMUWBUUSXBUWGUVAVUBUUBUUMUWBUUCXBWNXDXEXIXOXPXOXSXSXPXQXPXFXRX
        RXRXNXOXTSYAXNYBYCUUAUVDUVIDEIIUUADYDZUUAEYDZUVMUVCUVHFIUVMFYDZUVMUVNUM
        ZUUTUVFUVBUVGVXFUUSIUFZUVNUVFYEUFZUUTUVFUIUVMVXGUVNUUAUVLVXGUUAVXGEIULD
        IULUVLVXGUPDEGHIJKLMNOPQRSYFVXGDEIIYGWAXFVIUVMUVNVQZVXHVXFUUSUUMUUBUUCU
        XDUXFYIUWIYIYJABCUUSUUMIIUUHUVFUUKUUSUUFUHYEUUDUUSUUFXCUUFUUMUUSXBUUJUU
        EUUGUMUUIUMZABCUUEUUGUUIYHYKZYLWGVXFUVJUVAIUFZUVGYEUFZUVBUVGUIUUAUVJUVK
        UVNWEZUUAUVLUVNVXLUVLUUAUVNVXLUPZUVKUUAVXOUPUVJUUAUVKVXOUUAUVKUVNVXLUUA
        VXLFIULEIULUVKUVNUMVXLUPEFGHIJKLMNOPQRSYFVXLEFIIYGWAXIXOVRXOYMVXMVXFUUB
        UVAUXDUUCUUMUXFUWIYIZYIYJABCUUBUVAIIUUHUVGUUKUUBUUFUHZYEUUDUUBUUFXCZUUF
        UVAUUBXBVXKYLWGXDYNYOYPUUAUURUVDDEIIVXCVXDUVMUUQUVCFIVXEVXFUUNUUTUUPUVB
        VXFUULUUSUUMUUKVXFUVJUVKUUSYEUFZUULUUSUIVXNUUAUVJUVKUVNWIZUVLVXSUUAUVNU
        UBUUCIIYQWCABCUUBUUCIIUUHUUSUUKVXQYEVXRUUFUUCUUBXBVXKYLWGVGVXFUUOUVAUUB
        UUKVXFUVKUVNUVAYEUFZUUOUVAUIVXTVXIVYAVXFVXPYJUVKUVNVYAUJUUOUUCUUMVXJABC
        UKZTUVAUUKVYBUUCUUMVXKYRABCUUCUUMIIUUHUVAVYBUUCUUFUHYEUUDUUCUUFXCUUFUUM
        UUCXBVYBWHYLYSWGYTXDYNYOYP $.
        $( [29-Jun-2012] $)
    $}

    ${
      $d G a b f o p u v x y z $.  $d H f x y z $.  $d K a b f p $.
      $d X a b f o p u x y z $.

      $( Lemma for ~ grpohm .  Neutral element . $)
      grpohlem3 $p |- ( ( K e. TopGrp /\ ( x e. X |-> ( ( ( Id ` G ) G
                         x ) G ( Id ` G ) ) ) e. H ) -> A. f e. H ( (
                         x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G )
                         ) ) { <. <. x , y >. , z >. | ( x e. H /\
                         y e. H /\ z = ( x o. y ) ) } f ) = f ) $=
        ( co wcel wceq vu vv cgi cfv cv cmpt ctopgrp ccom w3a copab2 wral ax-17
        weq oveq2 oveq1d cbvmpt eleq1i wa wi oveq1i cvv wrex cab topgrpgrp eqid
        cgr grpoidcl syl adantr a1i simpl mpteq2dva eqeq2d rcla42ev syl3anc crn
        oveq2d c1st fvex eqeltri rnex mptexg ax-mp eqeq1 rexbidv elab syl6eleqr
        sylibr simpr coex coeq1 coeq2 df-3an oprabbii ov2g adantl simpll simplr
        vex cmprltr2 syl122anc eqidd grpolid impcom adantlr grporid imp oveq12d
        com12 mpteq12dv eqtrd eqeq12d imbi2d syl5ibrcom rexlimivv eleq2s syl5eq
        ex id sylbi ralrimiv sylan2b ) AJFUCUDZAUEZFRZYCFRZUFZGSIUGSZUAJYCUAUEZ
        FRZYCFRZUFZGSZYGDUEZYDGSZBUEZGSZCUEYDYPUHZTZUIZABCUJZRZYNTZDGUKYGYLGAUA
        UBJYFYKUBUEZYFSUAULUUDYKSZAULAUAUMYEYJYCFYDYIYCFUNUOUPZUQYHYMURUUCDGYHY
        NGSZUUCUSYMYHUUGUUCYHUUGURZUUBYLYNUUARZYNYGYLYNUUAUUFUTUUHUUIYLYNUHZYNU
        UHYMUUGUUJVASZUUIUUJTUUHYLEUEZKJLUEZKUEZFRZMUEZFRZUFZTZMJVBZLJVBZEVCZGU
        UHYLUURTZMJVBZLJVBZYLUVBSUUHYCJSZUVFYLKJYCUUNFRZYCFRZUFZTZUVEYHUVFUUGYH
        FVFSZUVFFIOVDZYCFJNYCVEZVGVHZVIZUVOUVJUUHUAKUBJYKUVHUUEKULUUDUVHSUAULUA
        KUMYJUVGYCFYIUUNYCFUNUOUPVJUVCUVJYLKJUVGUUPFRZUFZTLMYCYCJJUUMYCTZUURUVQ
        YLUVRKJUUQUVPUVRUUNJSZURZUUOUVGUUPFUVTUUMYCUUNFUVRUVSVKUOUOVLVMUUPYCTZU
        VQUVIYLUWAKJUVPUVHUWAUVSURUUPYCUVGFUWAUVSVKVQVLVMVNVOUVAUVEEYLJVASYLVAS
        JFVPVANFFIVRUDVAOIVRVSVTWAVTUAJYKVAWBWCZUULYLTZUUTUVDLJUWCUUSUVCMJUULYL
        UURWDWEWEWFWHQWGYHUUGWIUUKUUHYLYNUWBDWSZWJVJABCYLYNGGYRUUJUUAYLYPUHVAYD
        YLYPWKYPYNYLWLYTYOYQURYSURABCYOYQYSWMWNWOVOUUGYHUUJYNTZYHUWEUSZYNUVBGYN
        UVBSYNUURTZMJVBZLJVBZUWFUVAUWIEYNUWDEDUMZUUTUWHLJUWJUUSUWGMJUULYNUURWDW
        EWEWFUWGUWFLMJJUUMJSZUUPJSZURZUWFUWGYHYLUURUHZUURTZUSUWMYHUWOUWMYHURZUW
        NKJYCUUMFRZUUNFRZUUPYCFRZFRZUFZUURUWPUVKUVFUWKUVFUWLUWNUXATYHUVKUWMUVLW
        PYHUVFUWMUVNWPZUWKUWLYHWQUXBUWKUWLYHWRKUAYCUUMYCUUPUURYLFJYLVEUURVENWTX
        AUWPKJUWTJUUQUWPJXBUWPUWRUUOUWSUUPFUWKYHUWRUUOTUWLUWKYHURUWQUUMUUNFYHUW
        KUWQUUMTZYHUVKUWKUXCUSUVLUVKUWKUXCUUMYCFJNUVMXCXRVHXDUOXEUWMYHUWSUUPTZU
        WLYHUXDUSUWKYHUWLUXDYHUVKUWLUXDUSUVLUVKUWLUXDUUPYCFJNUVMXFXRVHXIWPXGXHX
        JXKXRUWGUWEUWOYHUWGUUJUWNYNUURYNUURYLWLUWGXSXLXMXNXOXTQXPXDXKXQXRVIYAYB
        $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d o p G $.  $d h H $.  $d a b h o p J $.  $d a b h p K $.  $d b o p X $.
      $( Lemma for ~ grpohm .  Homeomorphisms . $)
      grpohmlem4 $p |- ( K e. TopGrp -> H C_ ( J Homeo J ) ) $=
        ( wcel cv cima wral wi wceq wrex vh ctopgrp chomeosm cuni wf1o ccnv w3a
        co cmpt cab vex eqeq1 rexbidv elab crn topgrpbs eqtr cgr topgrpgrp eqid
        wa rltrooo 3exp syl wb f1oeq23 anidms imbi2d syl5ibr eqcoms mpcom com3l
        mpan 3imp ctop topgrptop jca 3ad2ant3 lrtrhom hmeocnb sylc hmeocna 3jca
        3coml 3expia f1oeq1 imaeq1 ralbidv cnveq 3anbi123d syl5ibrcom rexlimivv
        eleq1d imaeq1d sylbi eleq2s com12 ishomeo syl2anc sylibrd ssrdv ) EUBNZ
        UACDDUCUHZXBUAOZCNZDUDZXFXDUEZXDAOZPZDNZADQZXDUFZXHPZDNZADQZUGZXDXCNZXE
        XBXPXBXPRZXDXHGFHOZGOBUHIOZBUHUIZSZIFTZHFTZAUJZCXDYENXDYASZIFTZHFTZXRYD
        YHAXDUAUKXHXDSZYCYGHFYIYBYFIFXHXDYAULUMUMUNYFXRHIFFXSFNZXTFNZVAXRYFXBXF
        XFYAUEZYAXHPZDNZADQZYAUFZXHPZDNZADQZUGZRYJYKXBYTYJYKXBUGZYLYOYSYJYKXBYL
        XBYJYKYLBUOZXFSZXBYJYKYLRZRZBDEKLUPFUUBSZUUCXBUUERZJUUFUUCVAFXFSUUGFUUB
        XFUQUUGXFFXBUUEXFFSZYJYKFFYAUEZRZRZXBBURNZUUKBEKUSUULYJYKUUIGXSXTYABFYA
        UTZJVBVCVDUUHUUDUUJYJUUHYLUUIYKUUHYLUUIVEXFFXFFYAVFVGVHVHVIVJVDVMVKVLVN
        UUADVONZUUNVAZYAXCNZYOXBYJUUOYKXBUUNUUNDELVPZUUQVQVRXBYJYKUUPGXSXTYABDE
        FJUUMKLVSWDZAYADDVTWAUUAUUOUUPYSUUAUUNUUNXBYJUUNYKUUQVRZUUSVQUURAYADDWB
        WAWCWEYFXPYTXBYFXGYLXKYOXOYSXFXFXDYAWFYFXJYNADYFXIYMDXDYAXHWGWMWHYFXNYR
        ADYFXMYQDYFXLYPXHXDYAWIWNWMWHWJVHWKWLWOMWPWQXBUUNUUNXQXPVEUUQUUQAXDDDXF
        XFXFUTZUUTWRWSWTXA $.
        $( [12-Jul-2012] $)
    $}

    ${
      $d G a b g p u x y z $.  $d G a b o p u $.  $d G b g p u v x y z $.
      $d G p u v w x $.  $d H g u v x y z $.  $d K f u v $.  $d K u v x $.
      $d X a b g p u x y z $.  $d X a b o p u $.  $d X b g p u v x y z $.
      $d a b f g u $.  $d f o u v $.
      $( Lemma for ~ grpohm .  Inverse element . $)
      grpohlem5 $p |- ( K e. TopGrp -> A. f e. H E. g e. H ( g { <.
      <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) }
      f ) = ( x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G ) ) ) ) $=
        ( wcel co vu vv vw ctopgrp cv ccom wceq w3a copab2 cgi cfv cmpt wrex wi
        cab vex wb wa simpl eqeq1d rexbidva adantr elab oveq1d mpteq2dva eqeq2d
        weq oveq2d cbvrex2v cgn cvv cgr topgrpgrp adantl eqid grpoinvcl syl2anc
        simpll simplr crn rnexg syl syl5eqel grphmlem0 ov2gc cmprltr2 syl122anc
        syl3anc cbvmpt grpolinv ad2antlr simpllr grporinv oveq12d syl5eq 3eqtrd
        ax-17 oveq2 oveq1 rcla4ev expcom com12 rexbidv imbi2d com3l mpcom com23
        syl5ibr ex rexlimivv sylbi eleq2s ralrimiv ) JUDSZEUEZDUEZAUEZHSBUEZHSC
        UEXQXRUFUGUHABCUIZTZAKGUJUKZXQGTZYAGTZULZUGZEHUMZDHXPHSXNYFXNYFUNZXPFUE
        ZLKMUEZLUEZGTZNUEZGTZULZUGZNKUMZMKUMZFUOZHXPYRSXPYNUGZNKUMZMKUMZYGYQUUA
        FXPDUPFDVGZYPYTMKUUBYPYTUQYIKSUUBYOYSNKUUBYLKSZURYHXPYNUUBUUCUSUTVAVBVA
        VCUUAXPLKUAUEZYJGTZUBUEZGTZULZUGZUBKUMUAKUMYGYSUUIXPLKUUEYLGTZULZUGMNUA
        UBKKMUAVGZYNUUKXPUULLKYMUUJUULYJKSZURZYKUUEYLGUUNYIUUDYJGUULUUMUSVDVDVE
        VFNUBVGZUUKUUHXPUUOLKUUJUUGUUOUUMURYLUUFUUEGUUOUUMUSVHVEVFVIUUIYGUAUBKK
        UUDKSZUUFKSZURZXNUUIYFUURXNUUIYFUNZLKUUDGVJUKZUKZYJGTUUFUUTUKZGTULZHSZU
        URXNURZUUSUVEUVAKSZUVBKSZKVKSZUVDUVEGVLSZUUPUVFXNUVIUURGJPVMZVNZUUPUUQX
        NVRZUUDGUUTKOUUTVOZVPVQZUVEUVIUUQUVGUVKUUPUUQXNVSZUUFGUUTKOUVMVPVQZUVEK
        GVTZVKOUVEUVIUVQVKSUVKGVLWAWBWCZLUVAUVBVKFGHKMNRWDWHZUUIUVDUVEYFUVDUVEY
        FUNUUIUVEXOUUHXSTZYDUGZEHUMZUNUVEUVDUWBUVEUVCUUHXSTZYDUGZUVDUWBUNUVEUWC
        UVCUUHUFZLKUVAUUDGTZYJGTZUUFUVBGTZGTZULZYDUVEUVDUUHHSZUWCUWEUGUVSUVEUUP
        UUQUVHUWKUVLUVOUVRLUUDUUFVKFGHKMNRWDWHABCUVCUUHHHXSXSVOWEVQUVEUVIUVFUUP
        UVGUUQUWEUWJUGUVKUVNUVLUVPUVOLLUVAUUDUVBUUFUUHUVCGKUVCVOUUHVOOWFWGUVEUW
        JAKUWFXQGTZUWHGTZULYDLAUCKUWIUWMUCUEZUWISAWQUWNUWMSLWQLAVGUWGUWLUWHGYJX
        QUWFGWRVDWIUVEAKUWMYCUVEXQKSZURZUWLYBUWHYAGUWPUWFYAXQGUVEUWFYAUGZUWOUVE
        UVIUUPUWQUVKUVLUUDYAGUUTKOYAVOZUVMWJVQVBVDUWPUVIUUQUWHYAUGXNUVIUURUWOUV
        JWKUUPUUQXNUWOWLUUFYAGUUTKOUWRUVMWMVQWNVEWOWPUVDUWDUWBUWAUWDEUVCHXOUVCU
        GUVTUWCYDXOUVCUUHXSWSUTWTXAWBXBUUIYFUWBUVEUUIYEUWAEHUUIXTUVTYDXPUUHXOXS
        WRUTXCXDXHXEXFXIXGXJXKXKRXLXBXM $.
        $( [16-Jul-2012] $)
    $}

    ${
      $d a b f g h o u x y z H $.  $d a a0 a1 b b0 b1 f g o p q u v x y z X $.
      $d a a0 a1 b b0 b1 f g o p q u v w x y z G $.  $d a b o p J $.
      $d a a0 a1 b b0 b1 f g h o p u x y K $.
      $( The functions of equation ` ( ( a G p ) G b ) ` form a group of
         homeomorphisms.  (Contributed by FL, 17-Jul-2012.) $)
      grpohm $p |- ( K e. TopGrp -> ( { <. <. x , y >. , z >. |
         ( x e. H /\ y e. H /\ z = ( x o. y ) ) } e. GrpOp /\
         H C_ ( J Homeo J ) ) ) $=
        ( wcel wceq co wa vf vg vh vu vv bnjva1 bnjvb1 bnjva0 bnjvb0 vq ctopgrp
        vw cv ccom w3a copab2 cgr chomeosm wss crn cxp wf wral wrex cmpt cab wb
        wex wal wi eqid weq oveq1 oveq1d adantr mpteq2dva eqeq2d oveq2 rcla42ev
        mp3an3 cvv topgrpgrp unsgrp syl adantl mptexg eqeq1 elabg mpbird syl6bb
        rexbidv cbvrexv syl6eleqr 3adant2 cfv cgi grpoidcl simpl eleq1d syl5ibr
        c1st ax-mp simpr grpocl syl3anc jca sylanb eqidd mpteq12dv com3r eleq2s
        3exp syl6 mpcom impcom 3jca sylib ax-17 rcla4e syl2anc df-3an rexlimivv
        3ad2ant3 com12 vex a17d oveq12d anbi12d ex cbvexd df-rex 3bitr4g cbvmpt
        anim12i a1i 3imp sylbi sylibr eqeltri raleqbidv abbii eqcomi eleq1i a1d
        fveq2 eqcomd fveq2i grporid grpolid eqtr2d com4t pm2.43i rneqi syl5eqel
        eqtr4i rnexg simp2 simp1 3adant2r simp2r fmpt crimmt1 eqtr4d coeq2 r2ex
        ralrimiv coeq1 bicomi 2exbii bitri eleq2i simpll simplr coeq12d simp2ll
        3bitri simp2rl simp2rr simp2lr coeq1d cmprltr eqtrd biimpd oveq2d com4r
        syl6bi com23 com3l exlimdvv impbid alrimiv eq2ab 3anbi3d 2exbidv cbvabv
        syl6eq rnoprab 3eqtr4g grpohmlem1 oprabbii foprab2 grpohmlem2 fvex rnex
        elab c2nd grpohlem3 grpohlem5 r19.26 sylanbrc eqeq1d eqeq2 rcla4edv mpd
        ralbidv mpdan xpeq12 anidms mpancom id raleqdv rexeqdv anbi2d rexeqbidv
        feq23 3anbi123d eqcoms ab2rexex pm3.2i oprabex2g isgrpo grpohmlem4 ) HU
        KQZAUMZFQZBUMZFQZCUMZUYNUYPUNZRZUOZABCUPZUQQZFGGURSUSUYMVUCVUBUTZVUDVAZ
        VUDVUBVBZUAUMZUBUMZVUBSUCUMZVUBSVUGVUHVUIVUBSVUBSRZUCVUDVCZUBVUDVCZUAVU
        DVCZUDUMZVUGVUBSZVUGRZVUHVUGVUBSZVUNRZUBVUDVDZTZUAVUDVCZUDVUDVDZUOZFVUD
        RZUYMVVCUYMDUMZJIKUMZJUMZESZLUMZESZVEZRZLIVDZKIVDZDVFZVUABVHAVHZCVFZFVU
        DUYMVVOUYOUYQVVEUYSRZUOZBVHAVHZDVFZVVQUYMVVNVVTVGZDVIVVOVWARUYMVWBDUYMV
        VNVVTVVNUYMVVTVVLUYMVVTVJKLIIVVFIQZVVIIQZTZVVLUYMVVTVWEVVLUYMUOZVVRBFVD
        ZAFVDZVVTVWFVVKFQZVVEVVKUYPUNZRZBFVDZVWHVWEUYMVWIVVLVWEUYMTZVVKVVOFVWMV
        VKVVEJIVUNVVGESZUEUMZESZVEZRZUEIVDZUDIVDZDVFZVVOVWMVVKVXAQZVVKVWQRZUEIV
        DZUDIVDZVWEVXEUYMVWCVWDVVKVVKRZVXEVVKVKZVXCVXFVVKJIVVHVWOESZVEZRUDUEVVF
        VVIIIUDKVLZVWQVXIVVKVXJJIVWPVXHVXJVWPVXHRVVGIQZVXJVWNVVHVWOEVUNVVFVVGEV
        MVNVOVPVQUELVLZVXIVVKVVKVXLJIVXHVVJVXLVXHVVJRVXKVWOVVIVVHEVRVOVPVQVSVTV
        OVWMVVKWAQZVXBVXEVGVWMIWAQZVXMUYMVXNVWEUYMEUQQZVXNEHNWBZEIMWCWDWEJIVVJW
        AWFWDVWTVXEDVVKWAVVLVWSVXDUDIVVLVWRVXCUEIVVEVVKVWQWGWKWKWHWDWIVVNVWTDVV
        MVWSKUDIKUDVLZVVMVVEJIVWNVVIESZVEZRZLIVDVWSVXQVVLVXTLIVXQVVKVXSVVEVXQJI
        VVJVXRVXQVVJVXRRVXKVXQVVHVWNVVIEVVFVUNVVGEVMVNVOVPVQWKVXTVWRLUEILUEVLZV
        XSVWQVVEVYAJIVXRVWPVYAVXRVWPRVXKVVIVWOVWNEVRVOVPVQWLWJWLUUAWMPWMWNVWFJI
        VVGVEZFQVVEVVKVYBUNZRZVWLVWFVYBVVOFVWFVYBVVOQZVYBVVKRZLIVDZKIVDZVWEUYMV
        YHVVLUYMVWEVYHUYMHXAWOZUQQZVWEVYHVJZVYIHVYIVKWBVYIWPWOZVYIUTZQVYJVYKVYL
        VYIVYMVYMVKVYLVKWQVYJVYKVJZVYLIVYMVYLIQZVYNVYJVWEVYOVYOVYHVYJVWEVYBJIVY
        LVVGESZVYLESZVEZRZVYOVYOVYHVJVJVYJVYSVWEVYJJIVVGVYQVYJVXKTZVYQVYPVVGVYT
        VXOVYPIQZTZVYQVYPRVYJVXOVXKWUBVYIEUQEVYINUUBZUUCZVXOVXKTZVXOWUAVXOVXKWR
        ZWUEVXOVYOVXKWUAWUFEVYIRZWUEVYOVJNWUEVYOWUGEWPWOZIQZVXOWUIVXKWUHEIMWUHV
        KWQZVOWUGVYLWUHIWUGWUHVYLEVYIWPUUEUUFWSWTXBVXOVXKXCVYLVVGEIMXDXEXFXGVYP
        VYLEIMVYIEWPWUCUUGZUUHWDVYJVXOVXKVYPVVGRWUDVVGVYLEIMWUKUUIXGUUJVPUUDVYO
        VYOVYSVYHVYOVYOVYSVYHVYFVYSVYBJIVYPVVIESZVEZRKLVYLVYLIIVVFVYLRZVVKWUMVY
        BWUNJIVVJIWULWUNIXHWUNVVHVYPVVIEVVFVYLVVGEVMVNXIVQVVIVYLRZWUMVYRVYBWUOJ
        IWULIVYQWUOIXHVVIVYLVYPEVRXIVQVSXLXJXMUUKUULVYMEUTZIVYIEWUCUUMMUUOXKXNW
        DXOWNVWFVYBWAQZVYEVYHVGUYMVWEWUQVVLUYMVXNWUQUYMIWUPWAMUYMVXOWUPWAQVXPEU
        QUUPWDUUNJIVVGWAWFWDYCVVNVYHDVYBWAVVEVYBRZVVMVYGKIWURVVLVYFLIVVEVYBVVKW
        GWKWKWHWDWIPWMVWFVVEVVKVYCVWEVVLUYMUUQVWFIIVVKVBZVYCVVKRVWEUYMWUSVVLVWM
        VVJIQZJIVCWUSVWMWUTJIVWMVXKVXOVVHIQZVWDUOZWUTUYMVWEVXKWVBVJZUYMVXOVWEWV
        CVJVXPVXOVWEVXKWVBVXOVWEVXKUOVXOWVAVWDVXOVWEVXKUURVXOVWCVXKWVAVWDVVFVVG
        EIMXDUUSVXOVWCVWDVXKUUTXPXLWDXOVVHVVIEIMXDXMUVFJIIVVJVVKVXGUVAXQWNJIIVV
        KUVBWDUVCVWKVYDBVYBFVYDBXRUYPVYBRVWJVYCVVEUYPVYBVVKUVDVQXSXTVWGVWLAVVKF
        VWLAXRUYNVVKRZVVRVWKBFWVDUYSVWJVVEUYNVVKUYPUVGVQWKXSXTVWHUYOUYQTZVVRTZB
        VHAVHVVTVVRABFFUVEWVFVVSABVVSWVFUYOUYQVVRYAUVHUVIUVJXQXLYBYDUYMVVSVVNAB
        VVSUYMVVNUYOUYQVVRUYMVVNVJZUYQVVRWVGVJZVJZUYNVVOFUYNVVOQZWVDLIVDZKIVDZW
        VIUYNWAQWVJWVLVGAYEVVNWVLDUYNWADAVLZVVMWVKKIWVMVVLWVDLIVVEUYNVVKWGWKWKW
        HXBWVLUYNJIUFUMZVVGESZUGUMZESZVEZRZUGIVDZUFIVDZWVIWVKWVTKUFIVVFWVNRZVWD
        WVDTZLVHWVPIQZWVSTZUGVHWVKWVTWWBWWCWWELUGWWBUGXRWWBWWCUGYFWWBVVIWVPRZWW
        CWWEVGWWBWWFTZVWDWWDWVDWVSWWGVVIWVPIWWBWWFXCZWSWWGVVKWVRUYNWWGJIVVJWVQW
        WGVVJWVQRVXKWWGVVHWVOVVIWVPEWWGVVFWVNVVGEWWBWWFWRVNWWHYGVOVPVQYHYIYJWVD
        LIYKWVSUGIYKYLWLUYQWWAWVHUYQUYPJIUHUMZVVGESZUIUMZESZVEZRZUIIVDZUHIVDZWW
        AWVHVJZUYQUYPVVOQZUYPVVKRZLIVDZKIVDZWWPFVVOUYPPUVKUYPWAQWWRWXAVGBYEVVNW
        XADUYPWADBVLZVVMWWTKIWXBVVLWWSLIVVEUYPVVKWGWKWKWHXBWWTWWOKUHIVVFWWIRZVW
        DWWSTZLVHWWKIQZWWNTZUIVHWWTWWOWXCWXDWXFLUIWXCUIXRWXCWXDUIYFWXCVVIWWKRZW
        XDWXFVGWXCWXGTZVWDWXEWWSWWNWXHVVIWWKIWXCWXGXCWSWXHVVKWWMUYPWXHJIVVJWWLW
        XHVXKTZVVHWWJVVIWWKEWXIVVFWWIVVGEWXCWXGVXKUVLVNWXCWXGVXKUVMYGVPVQYHYIYJ
        WWSLIYKWWNUIIYKYLWLUVPWWNWWQUHUIIIWWAWWIIQZWXETZWWNWVHWVSWXKWWNWVHVJZVJ
        UFUGIIWVNIQZWWDTZWXKWVSWXLWXNWXKWVSWXLVJWVSWWNVVRWXNWXKTZWVGWVSWWNVVRWX
        OWVGVJZVJWVSWWNTZVVRVVEWVRWWMUNZRZWXPWXQUYSWXRVVEWXQUYNWVRUYPWWMWVSWWNW
        RWVSWWNXCUVNVQWXSWXOUYMVVNWXSWXOUYMUOZWVNWWIESZIQZWWKWVPESZIQZVVEJIWYAV
        VGESZWYCESZVEZRZVVNWXTVXOWXMWXJWYBUYMWXSVXOWXOVXPYCZWXMWWDWXKWXSUYMUVOW
        XJWXEWXNWXSUYMUVQWVNWWIEIMXDXEWXTVXOWXEWWDWYDWYIWXJWXEWXNWXSUYMUVRWXMWW
        DWXKWXSUYMUVSWWKWVPEIMXDXEWXSWXOUYMWYHWXOUYMWXSWYHWXOUYMWXSWYHVJWXOUYMT
        ZWXSWYHWYJWXRWYGVVEWYJVXOWXMWXJTZWWDWXETZWXRWYGRUYMVXOWXOVXPWEWXOWYKUYM
        WXNWXMWXKWXJWXMWWDWRWXJWXEWRYNVOWXOWYLUYMWXNWWDWXKWXEWXMWWDXCWXJWXEXCYN
        VOVXOWYKWYLUOZWXRUJIWVNUJUMZESZWVPESZVEZWWMUNWYGWYMWVRWYQWWMWVRWYQRWYMJ
        UJCIWVQWYPUYRWVQQUJXRUYRWYPQJXRJUJVLWVOWYOWVPEVVGWYNWVNEVRVNYMYOUVTJUJW
        VNWWIWVPWWKWWMWYQEIWYQVKWWMVKMUWAUWBXEVQUWCYIXJYPVVLWYHVVEJIWYEVVIESZVE
        ZRKLWYAWYCIIVVFWYARZVVKWYSVVEWYTJIVVJWYRWYTVVJWYRRVXKWYTVVHWYEVVIEVVFWY
        AVVGEVMVNVOVPVQVVIWYCRZWYSWYGVVEXUAJIWYRWYFXUAVXKTVVIWYCWYEEXUAVXKWRUWD
        VPVQVSXEXLUWFYIUWEYIUWGYBUWHYBYQYDYQYQPXKYPYDUWIUWJUWKVVNVVTDUWLYRVVTVV
        PDCDCVLZVVSVUAABXUBVVRUYTUYOUYQVVEUYRUYSWGUWMUWNUWOUWPPVUAABCUWQUWRUYMV
        VCVVDFFVAZFVUBVBZVUJUCFVCZUBFVCZUAFVCZVUPVURUBFVDZTZUAFVCZUDFVDZUOZUYMX
        UDXUGXUKUYMUYSFQBFVCAFVCXUDABDEFGHIJKLMNOPUWSABCFFUYSFVUBVUAWVEUYTTABCU
        YOUYQUYTYAUWTZUXAXQABCUAUBUCDEFGHIJKLMNOPUXBUYMAIWUHUYNESZWUHESZVEZFQZX
        UKUYMXUPVVOFUYMXUPVVKRZLIVDZKIVDZXUPVVOQUYMWUIWUIXUPJIWUHVVGESZWUHESZVE
        ZRZXUTUYMVXOWUIVXPWUJWDZXVEXVDUYMAJULIXUOXVBULUMZXUOQJXRXVFXVBQAXRAJVLX
        UNXVAWUHEUYNVVGWUHEVRVNYMYOXURXVDXUPJIXVAVVIESZVEZRKLWUHWUHIIVVFWUHRZVV
        KXVHXUPXVIJIVVJIXVGXVIIXHXVIVVHXVAVVIEVVFWUHVVGEVMVNXIVQVVIWUHRZXVHXVCX
        UPXVJJIXVGIXVBXVJIXHVVIWUHXVAEVRXIVQVSXEVVNXUTDXUPVXNXUPWAQIWUPWAMEEVYI
        WANHXAUXCYSUXDYSZAIXUOWAWFXBVVEXUPRZVVMXUSKIXVLVVLXURLIVVEXUPVVKWGWKWKU
        XEYRPWMUYMXUQTZXUPVUGVUBSZVUGRZVUQXUPRZUBFVDZTZUAFVCZXUKXVMXVOUAFVCXVQU
        AFVCZXVSABCUADEFHUXFWOZHIJKLMNXWAVKPUXGUYMXVTXUQABCUAUBDEFGHIJKLMNOPUXH
        VOXVOXVQUAFUXIUXJUYMXUJXVSUDXUPFVUNXUPRZXUJXVSVGUYMXWBXUIXVRUAFXWBVUPXV
        OXUHXVQXWBVUOXVNVUGVUNXUPVUGVUBVMUXKXWBVURXVPUBFVUNXUPVUQUXLWKYHUXOWEUX
        MUXNUXPXPVVCXULVGVUDFVUDFRZVUFXUDVUMXUGVVBXUKVUEXUCRZXWCVUFXUDVGXWCXWDV
        UDFVUDFUXQUXRVUEVUDXUCFVUBUYEUXSXWCVULXUFUAVUDFXWCUXTZXWCVUKXUEUBVUDFXW
        EXWCVUJUCVUDFXWEUYAYTYTXWCVVAXUJUDVUDFXWEXWCVUTXUIUAVUDFXWEXWCVUSXUHVUP
        XWCVURUBVUDFXWEUYBUYCYTUYDUYFUYGWTXNUYMVUBWAQZVUCVVCVGUYMFWAQZXWGTZXWFX
        WHUYMXWGXWGFVVOWAPKLDIIVVKXVKXVKUYHYSZXWIUYIYOABCFFUYSWAWAVUBXUMUYJWDUA
        UBUCUDWAVUBVUDVUDVKUYKWDWIDEFGHIJKLMNOPUYLXF $.
        $( [17-Jul-2012] $)
    $}
  $}

  $c opfn $.

  $( Extend class notation with an operator that derives an operation on
     functions from an operation on the elements of the common range of those
     functions. $)
  copfn $a class opfn $.


  ${
    $d g i h x y z a b $.
    $( Multiplication or addition of two functions ` x ` and ` y ` derived from
       the operation ` g ` on the elements of the common range of ` x ` and
       ` y ` .  The functions ` x ` and ` y ` must also have the same domain
       ` i ` . $)
    df-opfn $a |- opfn = { <. <. g , i >. , h >. |
       h = { <. <. x , y >. , z >. |
         ( x e. ( dom dom g ^m i ) /\ y e. ( dom dom g ^m i ) /\
       z = { <. a , b >. | ( a e. dom x /\ b = ( ( x ` a ) g ( y ` a ) ) ) } )
        }
        } $.
  $}

