$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topological groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TopGrp $.

  $( Extend class notation with the class of all topological groups. $)
  ctopgrp $a class TopGrp $.

  ${
    $d g j $.
    $( Define the class of all topological groups.  A topological group is a
       group whose operation and inverse function are continuous. $)
    df-topgrp $a |- TopGrp = { <. g , j >. | ( ( g e. Grp /\ j e. Top ) /\
         g e. ( ( j tX j ) Cn j ) /\ ( inv ` g ) e. ( j Cn j ) ) } $.
  $}

  ${
    $d G g j $.  $d J g j $.  $d x y $.
    $( The predicate "is a topological group".  Bourbaki TG III.1 $)
    istopgrp $p |- ( J e. A -> ( <. G , J >. e. TopGrp <->
      ( ( G e. Grp /\ J e. Top ) /\ G e. ( ( J tX J ) Cn J )
        /\ ( inv ` G ) e. ( J Cn J ) ) ) ) $=
      ( vx vy vg vj wcel cop ctopgrp cgr ctop wa ctx co ccn cgn cfv w3a cvv
      wbr df-br wrel cv copab relopab df-topgrp releqi mpbir brrelexi sylbir
      adantl pm3.26 jca elisset adantr 3ad2ant1 wceq eleq1 anbi1d fveq2 eleq1d
      3anbi123d anbi2d id opreq12d eleq2d opelopabg eleq2i syl5bb pm5.21nd ) CA
      HZBCIZJHZBKHZCLHZMZBCCNOZCPOZHZBQRZCCPOZHZSZBTHZVLMZVLVNMWEVLVNWEVLVNBCJU
      AWEBCJUBBCJJUCDUDZKHEUDZLHMWGWHWHNOWHPOHWGQRWHWHPOHSZDEUEZUCWIDEUFJWJDEUG
      UHUIUJUKULVLVNUMUNVLWDMWEVLWDWEVLVQVTWEWCVOWEVPBKUOUPUQULVLWDUMUNWFVMFUDZ
      KHZGUDZLHZMZWKWMWMNOZWMPOZHZWKQRZWMWMPOZHZSZFGUEZHWDVNXBVOWNMZBWQHZWAWTHZ
      SWDFGBCTAWKBURZWOXDWRXEXAXFXGWLVOWNWKBKUSUTWKBWQUSXGWSWAWTWKBQVAVBVCWMCUR
      ZXDVQXEVTXFWCXHWNVPVOWMCLUSVDXHWQVSBXHWPVRWMCPXHWMCWMCNXHVEZXIVFXIVFVGXHW
      TWBWAXHWMCWMCPXIXIVFVGVCVHJXCVMFGUGVIVJVK $.
      $( [18-Apr-2010] $)
  $}

  ${
    topgrpi.1 $e |- G = ( 1st ` K ) $.
    ${
      topgrpi.2 $e |- J = ( 2nd ` K ) $.
      ${
        $d G g j $.  $d J g j $.  $d K g j $.  $d N g j $.
        topgrpi.3 $e |- N = ( inv ` G ) $.
        $( "Axioms" of a topological group. $)
        topgrpi $p |- ( K e. TopGrp -> ( ( G e. Grp /\ J e. Top ) /\
          G e. ( ( J tX J ) Cn J ) /\ N e. ( J Cn J ) ) ) $=
          ( vg vj ctopgrp wcel cv cgr ctop wa ctx co ccn cgn cfv w3a copab
          df-topgrp eleq2i c1st wceq wb eqtr3 eqcomd eleq1 anbi1d fveq2
          syl6eqr eleq1d 3anbi123d syl mpan c2nd anbi2d opreq12 anidms mpancom
          eleq2d elopabi sylbi ) CJKCHLZMKZILZNKZOZVFVHVHPQZVHRQZKZVFSTZVHVHRQZ
          KZUAZHIUBZKAMKZBNKZOZABBPQZBRQZKZDBBRQZKZUAZJVRCHIUCUDVQVSVIOZAVLKZDV
          OKZUAZWGHICACUETZUFZVFWLUFZVQWKUGZEWMWNOZVFAUFZWOWPAVFAVFWLUHUIWQVJWH
          VMWIVPWJWQVGVSVIVFAMUJUKVFAVLUJWQVNDVOWQVNASTDVFASULGUMUNUOUPUQBCURTZ
          UFZVHWRUFZWKWGUGZFWSWTOZVHBUFZXAXBBVHBVHWRUHUIXCWHWAWIWDWJWFXCVIVTVSV
          HBNUJUSXCVLWCAVKWBUFZXCVLWCUFXCXDVHBVHBPUTVAVKWBVHBRUTVBVCXCVOWEDXCVO
          WEUFVHBVHBRUTVAVCUOUPUQVDVE $.
          $( [18-Apr-2010] $)

        $( In a topological group, the inverse function is continuous. $)
        topgrpinv $p |- ( K e. TopGrp -> N e. ( J Cn J ) ) $=
          ( ctopgrp wcel cgr ctop wa ctx co ccn topgrpi 3simp3d ) CHIAJIBKILABB
          MNBONIDBBONIABCDEFGPQ $.
          $( [21-Jun-2010] $)
      $}

      $( In a topological group, the base set of the topology and the base set
         of the group are equal. $)
      topgrpbs $p |- ( K e. TopGrp -> ran G = U. J ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv w3a crn cuni wceq eqid
        topgrpi wf1o wi grpinvf wf f1of cdm fdm eqtr2 ex syl5 syl cnf syl5com
        3exp pm2.43i com3r imp31 3adant2 ) CFGAHGZBIGZJZABBKLBMLGZANOZBBMLGZPAQ
        ZBRZSZABCVDDEVDTZUAVBVEVHVCUTVAVEVHUTVFVFVDUBZVAVEVHUCUCAVDVFVFTVIUDVAV
        EVJVHVAVEVJVHUCZUCVAVAVEVKVJVGVGVDUEZVHVAVAVEPVJVFVFVDUEZVLVHUCZVFVFVDU
        FVMVDUGZVFSZVNVFVFVDUHVPVOVGSZVHVLVPVQVHVOVFVGUIUJVGVGVDUHUKULULVDBBVGV
        GVGTZVRUMUNUOUPUQULURUSUL $.
        $( [21-Jun-2010] $)

      $( In a topological group, the operation ` G ` is continuous. $)
      topgrpcn $p |- ( K e. TopGrp -> G e. ( ( J tX J ) Cn J ) ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv eqid topgrpi 3simp2d ) CF
        GAHGBIGJABBKLBMLGANOZBBMLGABCSDESPQR $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the operation ` G ` is a group. $)
    topgrpgrp $p |- ( K e. TopGrp -> G e. Grp ) $=
      ( ctopgrp wcel cgr c2nd cfv ctop wa ctx co ccn cgn eqid topgrpi 3simp1d
      pm3.26d ) BDEZAFEZBGHZIEZSTUBJAUAUAKLUAMLEANHZUAUAMLEAUABUCCUAOUCOPQR $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrptop.1 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, ` J ` is a topology. $)
    topgrptop $p |- ( K e. TopGrp -> J e. Top ) $=
      ( ctopgrp wcel c1st cfv cgr ctop wa ctx co ccn cgn eqid topgrpi 3simp1d
      pm3.27d ) BDEZBFGZHEZAIEZSUAUBJTAAKLAMLETNGZAAMLETABUCTOCUCOPQR $.
      $( [21-Jun-2010] $)
  $}

  ${
    extopgrp.1 $e |- A e. B $.
    ${
      $d A x y $.
      $( The identity element of the trivial group. $)
      idtrgrp $p |- ( Id ` { <. <. A , A >. , A >. } ) = A $=
        ( vx vy cop csn cgi cfv cv co wceq wral crab cuni crn opex elisseti
        rnsnop eqcomi eqid grpsn grpidvallem wcel wa cab elsni adantr elsn
        biimpri copab fopabsn opreqi df-opr snid eqidd fvopab4 ax-mp 3eqtri
        opreq2 id 3eqtr4a syl rgen opreq1 eqeq1d ralbidv mpbiri jca impbii
        abbii df-rab df-sn 3eqtr4i unieqi unisn eqtri ) AAFZAFGZHIZDJZEJZVSKZWB
        LZEAGZMZDWENZOZAEDVTVSWEVSPWEVRAAAQZABCRZSTVTUAAWJUBUCWHWEOAWGWEWAWEUDZ
        WFUEZDUFWAALZDUFWGWEWLWMDWLWMWKWMWFWAAUGUHWMWKWFWKWMDAUIUJWMWFAWBVSKZWB
        LZEWEMWOEWEWBWEUDWBALZWOWBAUGWPAAVSKZAWNWBWQAAWAVRGZUDWPUEDEUKZKVRWSIZA
        VSWSAADEVRAWIWJULUMAAWSUNVRWRUDWTALVRWIUODEVRAAWRWSWAVRLAUPWSUAWJUQURUS
        WBAAVSUTWPVAVBVCVDWMWDWOEWEWMWCWNWBWAAWBVSVEVFVGVHVIVJVKWFDWEVLDAVMVNVO
        AWJVPVQVQ $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.
      $( The inverse function of the trivial group. $)
      invtrgrp $p |- ( inv ` { <. <. A , A >. , A >. } ) = ( _I |` { A } ) $=
        ( vx vy vz cop csn cgn cfv cv wcel co wceq crab cuni wa copab cid cres
        cgr elisseti grpsn crn opex rnsnop eqcomi cgi idtrgrp eqid grpinvfval
        ax-mp cxp restidsing fconstopab elsn pm3.26 opreq2d eqeq1d rabbidva
        unieqd df-opr fvsn eqtri wreu wb snid weu wal wex elsni adantr biimpri
        opreq1 syl6eq jca impbii ax-gen eqeq2 bibi2d albidv cla4egv mp2 df-eu
        mpbir df-reu reuuni2 bicomd mp2an syl6req sylbi eqeq2d pm5.32i opabbii
        3eqtrri ) AAGZAGHZIJZDKZAHZLZEKZFKZWSWQMZANZFWTOZPZNZQZDERZSWTTZWQUALWR
        XJNAABCUBZUCDFAEWQWRWTWQUDWTWPAAAUEZXLUFUGWQUHJAABCUIUGWRUJUKULXKWTWTUM
        XAXBANZQZDERXJAUNDEWTAUOXOXIDEXAXNXHXAAXGXBXAWSANZAXGNDAUPXPXGXCAWQMZAN
        ZFWTOZPZAXPXFXSXPXEXRFWTXPXCWTLZQZXDXQAYBWSAXCWQXPYAUQURUSUTVAXTANZAAWQ
        MZANZYDWPWQJAAAWQVBWPAXMXLVCVDZAWTLZXRFWTVEZYCYEVFAXLVGYHYAXRQZFVHZYJYI
        XCXBNZVFZFVIZEVJZABLYIXCANZVFZFVIZYNCYPFYIYOYAYOXRXCAVKVLYOYAXRYAYOFAUP
        VMYOXQYDAXCAAWQVNZYFVOVPVQVRYMYQEABXNYLYPFXNYKYOYIXBAXCVSVTWAWBWCYIFEWD
        WEXRFWTWFWEYGYHQYEYCXRYEFWTAYOXQYDAYRUSWGWHWIWEWJWKWLWMWNWOVD $.
        $( [21-Jun-2010] $)
    $}

    $( The trivial topological group. $)
    extopgrp $p |- <. { <. <. A , A >. , A >. } , { (/) , { A } } >.
       e. TopGrp $=
      ( cop csn c0 cpr ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv cvv w3a wb
      prex istopgrp ax-mp elisseti grpsn indistop pm3.2i cxp wf eqid txtop
      mp2an wfo crn opex rnsnop eqcomi grpfo fof syl cuni wceq cun 0ex snex
      unipr uncom un0 3eqtrri txuni mapudiscn cid cres invtrgrp idcn eqeltri
      mpbir3an ) AADZADEZFAEZGZDHIZVSJIZWAKIZLZVSWAWAMNZWAONIZVSPQZWAWAONZIZWAR
      IWBWEWGWJSTFVTUARVSWAUBUCWCWDAABCUDZUEZVTUFZUGWFKIZVTVTUHZVTVSUIZWGWDWDWN
      WMWMWAWAWFWFUJZUKULWCWPWLWCWOVTVSUMWPVSVTVSUNVTVRAAAUOWKUPUQURWOVTVSUSUTU
      CWOVTVSWFWFVAZWOWDWDWRWOVBWMWMWAWAWFVTVTWQWAVAFVTVCVTFVCVTFVTVDAVEZVFFVTV
      GVTVHVIZWTVJULUQWSVKULWHVLVTVMZWIABCVNWDXAWIIWMWAVTWTVOUCVPVQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrpsubcn.1 $e |- G = ( 1st ` K ) $.
    topgrpsubcn.2 $e |- J = ( 2nd ` K ) $.
    topgrpsubcn.3 $e |- D = ( /g ` G ) $.
    ${
      $d G u x y z $.  $d J u x y $.
      topgrpsubcn.4 $e |- K e. TopGrp $.
      $( Lemma for ~ topgrpsubcn . $)
      topgrpsubcnlem $p |- D e. ( ( J tX J ) Cn J ) $=
        ( vx vy vz vu cv crn wcel wa cgn cfv co wceq copab2 ctx ccn ctopgrp
        cgr topgrpgrp eqid grpdivfval syl ax-mp cop ccom cxp wfn wfo grpfo
        fofn 3syl pm3.26 wi grpinvcl ex adantl oprab2co ctop w3a txtop
        topgrptop sylanc 3jca cid cres cuni topgrpbs idcn topgrpinv 2txcn
        syl222anc fvresi adantr eqcomd opeq1d eqeq2d pm5.32i oprabbii syl5eqel
        topgrpcn jca32 cnco eqeltri ) AIMZBNZOZJMZWLOZPZKMWKWNBQRZRZBSTPIJKUAZC
        CUBSZCUCSZDUDOZAWSTZHXBBUEOZXCBDEUFZIJKABWQWLWLUGZWQUGZGUHUIUJWSBWPLMZW
        KWRUKZTZPZIJLUAZULZXAXBWSXMTZHXBBWLWLUMZUNZXNXBXDXOWLBUOXPXEBWLXFUPXOWL
        BUQURIJLKWLWLWKWRWLWLXLWSBWMWOUSWOWRWLOZWMXBWOXQUTZHXBXDXRXEXDWOXQWNBWQ
        WLXFXGVAVBUIUJVCXLUGWSUGVDUIUJWTVEOZXSCVEOZVFZXLWTWTUCSZOZBXAOZPPZXMXAO
        XBYEHXBYAYCYDXBXSXSXTXTXTXSXBCCWTWTUGZVGCDFVHZYGVIZYHYGVJXBWPXHWKVKWLVL
        ZRZWRUKZTZPZIJLUAZYBXLXBXTXTXTXTYICCUCSZOZWQYOOYNYBOYGYGYGYGXBXTYPYGCWL
        XBWLCVMTHBCDEFVNUJZVOUIBCDWQEFXGVPIJLCCWTYIWQYNCCWTWLWLYFYFYQYQYNUGVQVR
        XKYMIJLWPXJYLWPXIYKXHWPWKYJWRWPYJWKWMYJWKTWOWLWKVSVTWAWBWCWDWEWFBCDEFWG
        WHUJXLBWTWTCWIUJWJWJ $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the "subtraction" (or "division") is
       continuous.  Bourbaki TG III.1 axiom GT' $)
    topgrpsubcn $p |- ( K e. TopGrp -> D e. ( ( J tX J ) Cn J ) ) $=
      ( ctopgrp wcel c1st cfv cgs ctx co ccn c2nd c0 cop csn cpr cif wceq
      fveq2 fveq2d opreq12d eleq12d eqid cvv 0ex extopgrp elimel
      topgrpsubcnlem dedth opreq12i syl6eleqr fveq2i eqtri syl5eqel ) DHIZDJKZL
      KZCCMNZCONZAUSVADPKZVDMNZVDONZVCUSVAVFIUSDQQRQRSQQSTRZUAZJKZLKZVHPKZVKMNZ
      VKONZIDVGDVHUBZVAVJVFVMVNUTVILDVHJUCUDVNVEVLVDVKOVNVDVKVDVKMDVHPUCZVOUEVO
      UEUFVJVIVKVHVIUGVKUGVJUGDVGHQUHUIUJUKULUMVBVECVDOCVDCVDMFFUNFUNUOABLKVAGB
      UTLEUPUQUR $.
      $( [21-Jun-2010] $)
  $}

  ${
    trhom.1 $e |- X = ran G $.
    trhom.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trhom.3 $e |- G = ( 1st ` K ) $.
    trhom.4 $e |- J = ( 2nd ` K ) $.
    ${
      $d A x y $.  $d G x y $.  $d K x y $.  $d X x y $.
      $( In a topological group a right translation is a homeomorphism.
         Bourbaki TG III.2 $)
      trhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
        ( vy ctopgrp wcel wa chomeosm co cuni wf1o ccn ccnv w3a crn wceq wi
        topgrpbs wb eqtr f1oeq2 f1oeq3 bitrd eqcoms imbi2d syl mpan cgr
        topgrpgrp trooo ex syl5bir mpcom imp c2nd cfv opreq12 anidms eleq2d cv
        cop copab ccom cxp wral wfn eqid cmpt df-mpt df-opr eqeq2i anbi2i
        opabbii 3eqtri fnopabco2b opelxpi ancoms r19.21aiva adantl wfo grpfo
        fofn 3syl adantr sylanc ctop ctx topgrptop txtop 3jca eqcomi unieqi
        eqeq12i biimpi ttcn sylan eleq2 anbi2d ax-17 anbi1d opabbid eleq1d
        imbi12d mpbiri anabsi5 topgrpcn cnco sylan32c eqeltrd ax-mp cgs trinv
        eqtri grpdivfo topgrpsubcn cvv hmeobc wf fex oprex a1i rgen fopab2a
        mpbi c1st fvex eqeltri rnex sylancr syl3anc mpbird ) FMNZBGNZOZCEEPQNZE
        RZUUDCSZCEETQZNZCUAZUUFNZUBZUUBUUEUUGUUIYTUUAUUEDUCZUUDUDZYTUUAUUEUEZDE
        FJKUFZUULUUMUUAGGCSZUEZYTGUUKUDZUULUUMUUPUGZHUUQUULOGUUDUDZUURGUUKUUDUH
        UUSUUEUUOUUAUUEUUOUGUUDGUUDGUDUUEGUUDCSUUOUUDGUUDCUIUUDGGCUJUKULUMUNUOY
        TDUPNZUUPDFJUQZUUTUUAUUOABCDGIHURUSUNUTVAVBEFVCVDZUDZUUBUUGUEKUVCUUGCUV
        BUVBTQZNUUBUVCUUFUVDCUVCUUFUVDUDEUVBEUVBTVEVFZVGUUBCDAVHZGNZLVHZUVFBVIZ
        UDZOZALVJZVKZUVDUVIGGVLZNZAGVMZDUVNVNZCUVMUDUUBALGUVIUVNUVLCDUVLVOZCAGU
        VFBDQZVPUVGUVHUVSUDZOZALVJUVGUVHUVIDVDZUDZOZALVJIALGUVSVQUWAUWDALUVTUWC
        UVGUVSUWBUVHUVFBDVRVSVTWAWBWCUUAUVPYTUUAUVOAGUVGUUAUVOUVFBGGWDWEWFWGZYT
        UVQUUAYTUUTUVNGDWHUVQUVADGHWIUVNGDWJWKWLWMUVBWNNZUVBUVBWOQZWNNZUWFUBZUV
        LUVBUWGTQZNZDUWGUVBTQZNZUVMUVDNUUBYTUWIUUAYTUWFUWHUWFUVBFUVBVOZWPZUWFUW
        FUWHYTUVBUVBUWGUWGVOWQUWOUWOWMUWOWRWLZYTUUAUWKYTUULGUVBRZUDZUUBUWKUEZUU
        NUULUWRUUKGUUDUWQGUUKHWSEUVBKWTXAXBUWRUWSYTBUWQNZOZUVFUWQNZUVJOZALVJZUW
        JNZUEUWFUWTUXEYTALBUXDUVBUWQUWQVOUXDVOXCUWOXDUWRUUBUXAUWKUXEUWRUUAUWTYT
        GUWQBXEXFUWRUVLUXDUWJUWRUVKUXCALUWRAXGUWRLXGUWRUVGUXBUVJGUWQUVFXEXHXIXJ
        XKXLWKXMZYTUWMUUADUVBFJUWNXNWLUVLDUVBUWGUVBXOXPXQUTXRUUBUUHAGUVFBDXSVDZ
        QZVPZUUFUUTUUAUUHUXIUDYTABUXGCDGIHUXGVOZXTUVAXDUVCUUBUXIUUFNZUEKUVCUXKU
        XIUVDNUUBUVCUUFUVDUXIUVEVGUUBUXIUXGUVLVKZUVDUVPUXGUVNVNZUXIUXLUDUUBALGU
        VIUVNUVLUXIUXGUVRUXIUVGUVHUXHUDZOZALVJUVGUVHUVIUXGVDZUDZOZALVJALGUXHVQU
        XOUXRALUXNUXQUVGUXHUXPUVHUVFBUXGVRVSVTWAYAWCUWEYTUXMUUAYTUUTUVNGUXGWHUX
        MUVAUXGDGHUXJYBUVNGUXGWJWKWLWMUWIUWKUXGUWLNZUXLUVDNUUBUWPUXFYTUXSUUAUXG
        DUVBFJUWNUXJYCWLUVLUXGUVBUWGUVBXOXPXQUTXRXQWREWNNZUXTCYDNZUUCUUJUGUUBYD
        CEEUUDUUDUUDVOZUYBYEYTUXTUUAEFKWPWLZUYCGYDCYFZGYDNZUYAUUBGYDYDCYGUVSYDN
        ZAGVMUYDUYFAGUYFUVGUVFBDYHYIYJAGYDUVSCIYKYLUYEUUBGUUKYDHDDFYMVDYDJFYMYN
        YOYPYOYIYQYRYS $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.
      $( In a topological group, a converse of a right translation is a
         homeomorphism.  Bourbaki TG III.2 $)
      cnvtrhom $p |- ( ( K e. TopGrp /\ A e. X )
        -> `' F e. ( J Homeo J ) ) $=
        ( ctop wcel wa chomeosm co ccnv ctopgrp cnvhmpha topgrptop jca adantr
        trhom sylc ) ELMZUENZCEEOPZMCQUGMFRMZBGMZNCEESUHUFUIUHUEUEEFKTZUJUAUBAB
        CDEFGHIJKUCUD $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d F y $.  $d G x $.  $d J y $.  $d K x $.  $d O y $.
      $d X x $.
      $( In a topological group the image of an open set by a right translation
         is an open set.  Bourbaki TG III.2 $)
      trhom2 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
       -> ( F " O ) e. J ) $=
        ( vy ctopgrp wcel cima chomeosm co wa wi trhom cv wral ctop topgrptop
        jca adantr hmeocnb syl wceq imaeq2 eleq1d rcla4cv syl6com mpcom 3impia
        ) FNOZBHOZGEOZCGPZEOZCEEQROZUQURSZUSVATZABCDEFHIJKLUAVCVBCMUBZPZEOZMEUC
        ZVDVCEUDOZVISZVBVHTUQVJURUQVIVIEFLUEZVKUFUGMCEEUHUIVGVAMGEVEGUJVFUTEVEG
        CUKULUMUNUOUP $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.  $d O x $.
      tpgprop1.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by an element is
         open . $)
      tpgprop1 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
        -> ( O .t { A } ) e. J ) $=
        ( ctopgrp wcel w3a csn co cima cgr cpw wceq prsubrtr2 topgrpgrp
        3ad2ant1 3simp2 wi crn cuni wss topgrpbs eqtr ex pwuni pweq sseq2d
        mpbiri syl6 ax-mp syl sseld a1d 3imp syl3anc trhom2 eqeltrd ) FOPZBHPZG
        EPZQZGBRISZCGTZEDUAPZVIGHUBZPZVLVMUCVKABGCDIHKJNUDVHVIVNVJDFLUEUFVHVIVJ
        UGVHVIVJVPVHVJVPUHVIVHEVOGVHDUIZEUJZUCZEVOUKZDEFLMULHVQUCZVSVTUHJWAVSHV
        RUCZVTWAVSWBHVQVRUMUNWBVTEVRUBZUKEUOWBVOWCEHVRUPUQURUSUTVAVBVCVDVEABCDE
        FGHJKLMVFVG $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A y $.  $d G x y $.  $d J y $.  $d K x y $.  $d O x y $.  $d X x y $.
      tpgprop2.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by another set is
         open . $)
      tpgprop2 $p |- ( ( K e. TopGrp /\ A e. ~P X /\ O e. J )
        -> ( O .t A ) e. J ) $=
        ( vy ctopgrp wcel cpw w3a co cv csn ciun cgr cdm wceq eqid iscst4
        topgrpgrp 3ad2ant1 wa crn grprndm syl5req syl adantr pweq eleq2d cuni
        wss topgrpbs wi eqtr sseq2d ctop topgrptop topge syl5bir mpan mpcom
        sseld imp 3adant2 syl5eq biimpa 3adant3 syl3anc wral iunopn cmpt
        tpgprop1 3simpl1 elelpwi expcom 3ad2ant2 3simpl3 r19.21aiva sylanc
        eqeltrd ) FPQZBHRZQZGEQZSZGBITZOBGOUAZUBITZUCZEDUDQZGDUEUEZRZQZBXAQZWOW
        RUFWNOGBDIUDWTWTUGNUHWJWLWSWMDFLUIZUJWJWMXBWLWTHUFZWJWMUKZXBWJXEWMWJWSX
        EXDWSDULZWTHDUMZJUNUOUPXEXBGWKQZXFXEXAWKGWTHUQURWJWMXIWJEWKGXGEUSZUFZWJ
        EWKUTZDEFLMVAHXGUFZXKWJXLVBZJXMXKUKHXJUFZXNHXGXJVCXOXLEXJRZUTZWJXOWKXPE
        HXJUQVDWJEVEQZXQEFMVFZEXJXJUGVGUOVHUOVIVJVKVLVHVJVMWJWLXCWMWJWLXCWJWKXA
        BWJHWTUFWKXAUFWJXGWTHWJWSXGWTUFXDXHUOJVNHWTUQUOURVOVPVQXRWQEQZOBVRWREQW
        NOBWQEVSWJWLXRWMXSUJWNXTOBWJWPHQZWMXTWNWPBQZUKAWPAHAUAWPDTVTZDEFGHIJYCU
        GLMNWAWJWLWMYBWBWNYBYAWLWJYBYAVBWMYBWLYAWPBHWCWDWEVLWJWLWMYBWFVQWGWHWI
        $.
        $( [1-Jan-2011] $)
    $}
  $}

