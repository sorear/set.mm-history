$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topological groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TopGrp $.

  $( Extend class notation with the class of all topological groups. $)
  ctopgrp $a class TopGrp $.

  ${
    $d g j $.
    $( Define the class of all topological groups.  A topological group is a
       group whose operation and inverse function are continuous. $)
    df-topgrp $a |- TopGrp = { <. g , j >. | ( ( g e. GrpOp /\ j e. Top ) /\
         g e. ( ( j tX j ) Cn j ) /\ ( inv ` g ) e. ( j Cn j ) ) } $.
  $}

  ${
    $d G g j $.  $d J g j $.  $d x y $.
    $( The predicate "is a topological group".  Bourbaki TG III.1 (Contributed
       by FL, 18-Apr-2010.) $)
    istopgrp $p |- ( J e. A -> ( <. G , J >. e. TopGrp <->
      ( ( G e. GrpOp /\ J e. Top ) /\ G e. ( ( J tX J ) Cn J )
        /\ ( inv ` G ) e. ( J Cn J ) ) ) ) $=
      ( vx vy vg vj wcel ctopgrp cgr ctop wa ctx co ccn cgn cfv w3a cv eleq1 id
      cop cvv wbr df-br wrel copab relopab df-topgrp releqi mpbir sylbir adantl
      brrelexi simpl elisset adantr 3ad2ant1 eleq2i wceq anbi1d fveq2 3anbi123d
      jca eleq1d anbi2d opreq12d eleq2d opelopabg syl5bb pm5.21nd ) CAHZBCUBZIH
      ZBJHZCKHZLZBCCMNZCONZHZBPQZCCONZHZRZBUCHZVLLZVLVNLWEVLVNWEVLVNBCIUDWEBCIU
      EBCIIUFDSZJHESZKHLWGWHWHMNWHONHWGPQWHWHONHRZDEUGZUFWIDEUHIWJDEUIUJUKUNULU
      MVLVNUOVDVLWDLWEVLWDWEVLVQVTWEWCVOWEVPBJUPUQURUMVLWDUOVDVNVMFSZJHZGSZKHZL
      ZWKWMWMMNZWMONZHZWKPQZWMWMONZHZRZFGUGZHWFWDIXCVMFGUIUSXBVOWNLZBWQHZWAWTHZ
      RWDFGBCUCAWKBUTZWOXDWRXEXAXFXGWLVOWNWKBJTVAWKBWQTXGWSWAWTWKBPVBVEVCWMCUTZ
      XDVQXEVTXFWCXHWNVPVOWMCKTVFXHWQVSBXHWPVRWMCOXHWMCWMCMXHUAZXIVGXIVGVHXHWTW
      BWAXHWMCWMCOXIXIVGVHVCVIVJVK $.
      $( [18-Apr-2010] $)
  $}

  ${
    topgrpi.1 $e |- G = ( 1st ` K ) $.
    ${
      topgrpi.2 $e |- J = ( 2nd ` K ) $.
      ${
        $d G g j $.  $d J g j $.  $d K g j $.  $d N g j $.
        topgrpi.3 $e |- N = ( inv ` G ) $.
        $( "Axioms" of a topological group.  (Contributed by FL,
           18-Apr-2010.) $)
        topgrpi $p |- ( K e. TopGrp -> ( ( G e. GrpOp /\ J e. Top ) /\
          G e. ( ( J tX J ) Cn J ) /\ N e. ( J Cn J ) ) ) $=
          ( vg vj wcel cgr ctop wa ctx co ccn cgn cfv w3a wceq ctopgrp cv copab
          df-topgrp eleq2i wb eqtr3 eqcomd eleq1 anbi1d fveq2 syl6eqr 3anbi123d
          c1st eleq1d syl mpan c2nd anbi2d opreq12 anidms mpancom elopabi sylbi
          eleq2d ) CUAJCHUBZKJZIUBZLJZMZVFVHVHNOZVHPOZJZVFQRZVHVHPOZJZSZHIUCZJA
          KJZBLJZMZABBNOZBPOZJZDBBPOZJZSZUAVRCHIUDUEVQVSVIMZAVLJZDVOJZSZWGHICAC
          UNRZTZVFWLTZVQWKUFZEWMWNMZVFATZWOWPAVFAVFWLUGUHWQVJWHVMWIVPWJWQVGVSVI
          VFAKUIUJVFAVLUIWQVNDVOWQVNAQRDVFAQUKGULUOUMUPUQBCURRZTZVHWRTZWKWGUFZF
          WSWTMZVHBTZXAXBBVHBVHWRUGUHXCWHWAWIWDWJWFXCVIVTVSVHBLUIUSXCVLWCAVKWBT
          ZXCVLWCTXCXDVHBVHBNUTVAVKWBVHBPUTVBVEXCVOWEDXCVOWETVHBVHBPUTVAVEUMUPU
          QVCVD $.
          $( [18-Apr-2010] $)

        $( In a topological group, the inverse function is continuous.
           (Contributed by FL, 21-Jun-2010.) $)
        topgrpinv $p |- ( K e. TopGrp -> N e. ( J Cn J ) ) $=
          ( ctopgrp wcel cgr ctop wa ctx co ccn topgrpi simp3d ) CHIAJIBKILABBM
          NBONIDBBONIABCDEFGPQ $.
          $( [21-Jun-2010] $)
      $}

      $( In a topological group, the base set of the topology and the base set
         of the group are equal.  (Contributed by FL, 21-Jun-2010.) $)
      topgrpbs $p |- ( K e. TopGrp -> ran G = U. J ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn w3a wceq eqid wi wf fdm syl eqtr2
        cgn cfv crn cuni topgrpi wf1o grpinvf cnf cdm ex syl2im syl5com pm2.43i
        f1of 3exp com3r imp31 3adant2 ) CFGAHGZBIGZJZABBKLBMLGZAUBUCZBBMLGZNAUD
        ZBUEZOZABCVDDEVDPZUFVBVEVHVCUTVAVEVHUTVFVFVDUGZVAVEVHQQAVDVFVFPVIUHVAVE
        VJVHVAVEVJVHQZQVAVAVEVKVAVAVENVGVGVDRZVJVHVDBBVGVGVGPZVMUIVJVFVFVDRZVLV
        HQVFVFVDUOVNVDUJZVFOZVLVOVGOZVHVFVFVDSVGVGVDSVPVQVHVOVFVGUAUKULTUMUPUNU
        QTURUST $.
        $( [21-Jun-2010] $)

      $( In a topological group, the operation ` G ` is continuous.
         (Contributed by FL, 21-Jun-2010.) $)
      topgrpcn $p |- ( K e. TopGrp -> G e. ( ( J tX J ) Cn J ) ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv eqid topgrpi simp2d ) CFG
        AHGBIGJABBKLBMLGANOZBBMLGABCSDESPQR $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the operation ` G ` is a group.  (Contributed by
       FL, 21-Jun-2010.) $)
    topgrpgrp $p |- ( K e. TopGrp -> G e. GrpOp ) $=
      ( ctopgrp wcel cgr c2nd cfv ctop wa ctx co ccn eqid topgrpi simp1d simpld
      cgn ) BDEZAFEZBGHZIEZSTUBJAUAUAKLUAMLEARHZUAUAMLEAUABUCCUANUCNOPQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrptop.1 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, ` J ` is a topology.  (Contributed by FL,
       21-Jun-2010.) $)
    topgrptop $p |- ( K e. TopGrp -> J e. Top ) $=
      ( ctopgrp wcel c1st cfv cgr ctop wa ctx co ccn eqid topgrpi simp1d simprd
      cgn ) BDEZBFGZHEZAIEZSUAUBJTAAKLAMLETRGZAAMLETABUCTNCUCNOPQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    extopgrp.1 $e |- A e. _V $.
    $( The trivial topological group.  (Contributed by FL, 21-Jun-2010.)
       (Revised by Mario Carneiro, 15-Dec-2013.) $)
    extopgrp $p |- <. { <. <. A , A >. , A >. } , { (/) , { A } } >.
       e. TopGrp $=
      ( cop csn c0 cpr ctopgrp wcel cgr ctop wa ctx co ccn cvv ax-mp mp2an cuni
      eqcomi cun cgn cfv w3a wb prex istopgrp elisseti grpsn indistop pm3.2i wf
      cxp eqid txtop wfo crn opex rnsnop grpfo fof syl wceq 0ex unipr uncom un0
      snex 3eqtrri txuni mapudiscn cid cres ginvsn idcn eqeltri mpbir3an ) AACZ
      ACDZEADZFZCGHZVRIHZVTJHZKZVRVTVTLMZVTNMHZVRUAUBZVTVTNMZHZVTOHWAWDWFWIUCUD
      EVSUEOVRVTUFPWBWCAAOBUGZUHZVSUIZUJWEJHZVSVSULZVSVRUKZWFWCWCWMWLWLVTVTWEWE
      UMZUNQWBWOWKWBWNVSVRUOWOVRVSVRUPVSVQAAAUQWJURSUSWNVSVRUTVAPWNVSVRWEWERZWN
      WCWCWQWNVBWLWLVTVTWEVSVSWPVTREVSTVSETVSEVSVCAVGZVDEVSVEVSVFVHZWSVIQSWRVJQ
      WGVKVSVLZWHABVMWCWTWHHWLVTVSWSVNPVOVP $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    topgrpsubcn.1 $e |- G = ( 1st ` K ) $.
    topgrpsubcn.2 $e |- J = ( 2nd ` K ) $.
    topgrpsubcn.3 $e |- D = ( /g ` G ) $.
    ${
      $d G u x y z $.  $d J u x y $.
      topgrpsubcn.4 $e |- K e. TopGrp $.
      $( Lemma for ~ topgrpsubcn . $)
      topgrpsubcnlem $p |- D e. ( ( J tX J ) Cn J ) $=
        ( vx vy vz vu cv wcel wa co wceq eqid syl ax-mp crn cgn cfv ctx ctopgrp
        copab2 ccn cmpt2 cgr topgrpgrp grpdivfval df-mpt2 eqtri cop cxp wfn wfo
        ccom grpfo fofn 3syl simpl grpoinvcl adantl oprab2co ctop w3a topgrptop
        wi ex syl11anc 3jca cid cres fvresi adantr eqcomd opeq1d eqeq2d pm5.32i
        txtop oprabbii cuni topgrpbs idcn topgrpinv syl222anc syl5eqel topgrpcn
        2txcn jca32 cnco eqeltri ) AIMZBUAZNZJMZWONZOZKMWNWQBUBUCZUCZBPZQOIJKUF
        ZCCUDPZCUGPZAIJWOWOXBUHZXCDUENZAXFQZHXGBUINZXHBDEUJZIJABWTWOWORZWTRZGUK
        STIJKWOWOXBULUMXCBWSLMZWNXAUNZQZOZIJLUFZURZXEXGXCXRQZHXGBWOWOUOZUPZXSXG
        XIXTWOBUQYAXJBWOXKUSXTWOBUTVAIJLKWOWOWNXAWOWOXQXCBWPWRVBWRXAWONZWPXGWRY
        BVIZHXGXIYCXJXIWRYBWQBWTWOXKXLVCVJSTVDXQRXCRVESTXDVFNZYDCVFNZVGZXQXDXDU
        GPZNZBXENZOOZXRXENXGYJHXGYFYHYIXGYDYDYEXGYEYEYDCDFVHZYKCCXDXDRZWAVKZYMY
        KVLXGXQWSXMWNVMWOVNZUCZXAUNZQZOZIJLUFZYGXPYRIJLWSXOYQWSXNYPXMWSWNYOXAWS
        YOWNWPYOWNQWRWOWNVOVPVQVRVSVTWBXGYEYEYEYEYNCCUGPZNZWTYTNYSYGNYKYKYKYKXG
        YEUUAYKCWOXGWOCWCQHBCDEFWDTZWESBCDWTEFXLWFIJLCCXDYNWTYSCCXDWOWOYLYLUUBU
        UBYSRWJWGWHBCDEFWIWKTXQBXDXDCWLTWMWM $.
        $( [23-Dec-2013] $) $( [21-Jun-2010] $)
    $}

    $( In a topological group, the "subtraction" (or "division") is
       continuous.  Bourbaki TG III.1 axiom GT' (Contributed by FL,
       21-Jun-2010.) $)
    topgrpsubcn $p |- ( K e. TopGrp -> D e. ( ( J tX J ) Cn J ) ) $=
      ( ctopgrp wcel c1st cfv cgs ctx co ccn c2nd c0 cop csn eqid eqtri cpr cif
      fveq2i wceq fveq2 fveq2d opreq12d eleq12d 0ex elimel topgrpsubcnlem dedth
      extopgrp opreq12i syl6eleqr syl5eqel ) DHIZADJKZLKZCCMNZCONZABLKUTGBUSLEU
      DUAURUTDPKZVCMNZVCONZVBURUTVEIURDQQRQRSQQSUBRZUCZJKZLKZVGPKZVJMNZVJONZIDV
      FDVGUEZUTVIVEVLVMUSVHLDVGJUFUGVMVDVKVCVJOVMVCVJVCVJMDVGPUFZVNUHVNUHUIVIVH
      VJVGVHTVJTVITDVFHQUJUNUKULUMVAVDCVCOCVCCVCMFFUOFUOUPUQ $.
      $( [23-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    trhom.1 $e |- X = ran G $.
    trhom.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trhom.3 $e |- G = ( 1st ` K ) $.
    trhom.4 $e |- J = ( 2nd ` K ) $.
    ${
      $d A x y $.  $d G x y $.  $d K x y $.  $d X x y $.
      $( In a topological group a right translation is a homeomorphism.
         Bourbaki TG III.2 (Contributed by FL, 21-Jun-2010.) $)
      trhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
        ( vy wcel wa co wceq wi copab eqid cvv chomeosm cuni wf1o ccnv topgrpbs
        ctopgrp ccn w3a crn cgr topgrpgrp trooo ex syl eqtr f1oeq2 f1oeq3 bitrd
        eqcoms imbi2d mpan syl5ibr mpcom imp c2nd cfv cop ccom cxp wral opelxpi
        wb cv wfn ancoms r19.21aiva adantl grpfo fofn 3syl adantr df-mpt df-opr
        wfo cmpt eqeq2i anbi2i opabbii 3eqtri fnopabco2b syl11anc ctx topgrptop
        ctop txtop eqcomi unieqi eqeq12i biimpi sylan eleq2 anbi2d ax-17 anbi1d
        3jca ttcn opabbid eleq1d imbi12d anabsi5 topgrpcn cnco syl12anc eqeltrd
        mpbiri opreq12 anidms eleq2d ax-mp cgs trinv grpdivfo eqtri topgrpsubcn
        wf oprex a1i rgen fopab2a mpbi c1st fvex eqeltri rnex sylancr syl111anc
        fex hmeobc mpbird ) FUFMZBGMZNZCEEUAOMZEUBZUUDCUCZCEEUGOZMZCUDZUUFMZUHZ
        UUBUUEUUGUUIYTUUAUUEDUIZUUDPZYTUUAUUEQZDEFJKUEZYTUUMUULUUAGGCUCZQZYTDUJ
        MZUUPDFJUKZUUQUUAUUOABCDGIHULUMUNGUUKPZUULUUMUUPVLZHUUSUULNGUUDPZUUTGUU
        KUUDUOUVAUUEUUOUUAUUEUUOVLUUDGUUDGPUUEGUUDCUCUUOUUDGUUDCUPUUDGGCUQURUSU
        TUNVAVBVCVDEFVEVFZPZUUBUUGQKUUBUUGUVCCUVBUVBUGOZMUUBCDAVMZGMZLVMZUVEBVG
        ZPZNZALRZVHZUVDUUBUVHGGVIZMZAGVJZDUVMVNZCUVLPUUAUVOYTUUAUVNAGUVFUUAUVNU
        VEBGGVKVOVPVQZYTUVPUUAYTUUQUVMGDWDUVPUURDGHVRUVMGDVSVTWAALGUVHUVMUVKCDU
        VKSZCAGUVEBDOZWEUVFUVGUVSPZNZALRUVFUVGUVHDVFZPZNZALRIALGUVSWBUWAUWDALUV
        TUWCUVFUVSUWBUVGUVEBDWCWFWGWHWIWJWKUUBUVBWNMZUVBUVBWLOZWNMZUWEUHZUVKUVB
        UWFUGOZMZDUWFUVBUGOZMZUVLUVDMYTUWHUUAYTUWEUWGUWEUVBFUVBSZWMZYTUWEUWEUWG
        UWNUWNUVBUVBUWFUWFSWOWKUWNXEWAZYTUUAUWJYTUULGUVBUBZPZUUBUWJQZUUNUULUWQU
        UKGUUDUWPGUUKHWPEUVBKWQWRWSUWQUWRYTBUWPMZNZUVEUWPMZUVINZALRZUWIMZQYTUWE
        UWSUXDUWNALBUXCUVBUWPUWPSUXCSXFWTUWQUUBUWTUWJUXDUWQUUAUWSYTGUWPBXAXBUWQ
        UVKUXCUWIUWQUVJUXBALUWQAXCUWQLXCUWQUVFUXAUVIGUWPUVEXAXDXGXHXIXOVTXJZYTU
        WLUUADUVBFJUWMXKWAUVKDUVBUWFUVBXLXMXNUVCUUFUVDCUVCUUFUVDPEUVBEUVBUGXPXQ
        ZXRVBXSUUBUUHAGUVEBDXTVFZOZWEZUUFYTUUQUUAUUHUXIPUURABUXGCDGIHUXGSZYAWTU
        VCUUBUXIUUFMZQKUUBUXKUVCUXIUVDMUUBUXIUXGUVKVHZUVDUUBUVOUXGUVMVNZUXIUXLP
        UVQYTUXMUUAYTUUQUVMGUXGWDUXMUURUXGDGHUXJYBUVMGUXGVSVTWAALGUVHUVMUVKUXIU
        XGUVRUXIUVFUVGUXHPZNZALRUVFUVGUVHUXGVFZPZNZALRALGUXHWBUXOUXRALUXNUXQUVF
        UXHUXPUVGUVEBUXGWCWFWGWHYCWJWKUUBUWHUWJUXGUWKMZUXLUVDMUWOUXEYTUXSUUAUXG
        DUVBFJUWMUXJYDWAUVKUXGUVBUWFUVBXLXMXNUVCUUFUVDUXIUXFXRVBXSXNXEUUBEWNMZU
        XTCTMZUUCUUJVLYTUXTUUAEFKWMWAZUYBUUBGTCYEZGTMZUYAUVSTMZAGVJUYCUYEAGUYEU
        VFUVEBDYFYGYHAGTUVSCIYIYJUYDUUBGUUKTHDDFYKVFTJFYKYLYMYNYMYGGTTCYQYOTCEE
        UUDUUDUUDSZUYFYRYPYS $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.
      $( In a topological group, a converse of a right translation is a
         homeomorphism.  Bourbaki TG III.2 (Contributed by FL, 30-Dec-2010.) $)
      cnvtrhom $p |- ( ( K e. TopGrp /\ A e. X )
        -> `' F e. ( J Homeo J ) ) $=
        ( ctopgrp wcel wa ctop chomeosm co ccnv topgrptop jca adantr trhom sylc
        cnvhmpha ) FLMZBGMZNEOMZUGNZCEEPQZMCRUIMUEUHUFUEUGUGEFKSZUJTUAABCDEFGHI
        JKUBCEEUDUC $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d F y $.  $d G x $.  $d J y $.  $d K x $.  $d O y $.
      $d X x $.
      $( In a topological group the image of an open set by a right translation
         is an open set.  Bourbaki TG III.2 (Contributed by FL,
         30-Dec-2010.) $)
      trhom2 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
       -> ( F " O ) e. J ) $=
        ( vy ctopgrp wcel cima chomeosm co wa wi trhom cv wral topgrptop adantr
        ctop jca hmeocnb syl wceq imaeq2 eleq1d rcla4cv syl6com mpcom 3impia )
        FNOZBHOZGEOZCGPZEOZCEEQROZUQURSZUSVATZABCDEFHIJKLUAVCVBCMUBZPZEOZMEUCZV
        DVCEUFOZVISZVBVHTUQVJURUQVIVIEFLUDZVKUGUEMCEEUHUIVGVAMGEVEGUJVFUTEVEGCU
        KULUMUNUOUP $.
        $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.  $d O x $.
      tpgprop1.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by an element is
         open .  (Contributed by FL, 30-Dec-2010.) $)
      tpgprop1 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
        -> ( O .t { A } ) e. J ) $=
        ( ctopgrp wcel w3a cpw wceq wss csn co cima topgrpgrp 3ad2ant1 simp2 wi
        cgr crn cuni topgrpbs eqtr ex pwuni pweq sseq2d mpbiri syl6 mpsyl sseld
        a1d 3imp prsubrtr2 syl111anc trhom2 eqeltrd ) FOPZBHPZGEPZQZGBUAIUBZCGU
        CZEVJDUHPZVHGHRZPZVKVLSVGVHVMVIDFLUDUEVGVHVIUFVGVHVIVOVGVIVOUGVHVGEVNGH
        DUIZSZVGVPEUJZSZEVNTZJDEFLMUKVQVSHVRSZVTVQVSWAHVPVRULUMWAVTEVRRZTEUNWAV
        NWBEHVRUOUPUQURUSUTVAVBABGCDIHKJNVCVDABCDEFGHJKLMVEVF $.
        $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $}

    ${
      $d A y $.  $d G x y $.  $d J y $.  $d K x y $.  $d O x y $.  $d X x y $.
      tpgprop2.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by another set is
         open .  (Contributed by FL, 1-Jan-2011.)  (Revised by Mario Carneiro,
         15-Dec-2013.) $)
      tpgprop2 $p |- ( ( K e. TopGrp /\ A e. ~P X /\ O e. J )
        -> ( O .t A ) e. J ) $=
        ( vy wcel cpw co wceq 3ad2ant1 ctopgrp w3a cv csn cgr cdm topgrpgrp wss
        ciun cuni pwuni a1i crn topgrpbs syl5eq pweq syl sseqtr4d ssel2 3adant2
        sylan grprndm syl5req eleqtrrd simp2 eqid syl111anc ctop wral topgrptop
        iscst4 wa simpl1 elelpwi expcom 3ad2ant2 imp simpl3 tpgprop1 r19.21aiva
        wi cmpt iunopn syl11anc eqeltrd ) FUAPZBHQZPZGEPZUBZGBIRZOBGOUCZUDIRZUI
        ZEWJDUEPZGDUFUFZQZPBWQPWKWNSWFWHWOWIDFLUGZTWJGWGWQWFWIGWGPZWHWFEWGUHWIW
        SWFEEUJZQZWGEXAUHWFEUKULWFHWTSWGXASWFHDUMZWTJDEFLMUNUOHWTUPUQUREWGGUSVA
        UTWJWPHSZWQWGSWFWHXCWIWFWOXCWRWOHXBWPJDVBVCUQTWPHUPUQZVDWJBWGWQWFWHWIVE
        XDVDOGBDIUEWPWPVFNVKVGWJEVHPZWMEPZOBVIWNEPWFWHXEWIEFMVJTWJXFOBWJWLBPZVL
        WFWLHPZWIXFWFWHWIXGVMWJXGXHWHWFXGXHWAWIXGWHXHWLBHVNVOVPVQWFWHWIXGVRAWLA
        HAUCWLDRWBZDEFGHIJXIVFLMNVSVGVTOBWMEWCWDWE $.
        $( [15-Dec-2013] $) $( [1-Jan-2011] $)
    $}

  $}

  ${
    $d A x y $.  $d G x y $.  $d J x y $.  $d X x y $.
    ltrhom.1 $e |- X = ran G $.
    ltrhom.2 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrhom.3 $e |- G = ( 1st ` K ) $.
    ltrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a left translation is a homeomorphism. ` A ` is
       constant.  (Contributed by FL, 12-Jul-2012.) $)
    ltrhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
      ( vy wcel wa co ccn wceq adantr sylan eqid ctopgrp chomeosm cuni wf1o w3a
      crn topgrpbs syl5eq cgr topgrpgrp ltrooo wb f1oeq23 anidms eqcoms syl5ibr
      ccnv mpcom cop cmpt ccom copab ltrcmp df-mpt eqcomi a1i coeq2d eqtrd ctop
      cv topgrptop txtop syl11anc wi eqtr ttcn2 eleq2 anbi2d id eqidd mpteq12dv
      ctx eleq1d imbi12d mpbiri syl mpan topgrpcn cnco syl32anc eqeltrd cgn cfv
      ltrinvlem grpoinvcl syl5req eleqtrrd anbi1d opabbidv mpbird ltrset hmeobc
      cgs 3jca cvv syl111anc ) FUAMZBGMZNZCEEUBOMZEUCZXKCUDZCEEPOZMZCUQZXMMZUEZ
      XIXLXNXPGXKQZXIXLXGXRXHXGGDUFZXKHDEFJKUGZUHRZXIXLXRGGCUDZXGDUIMZXHYBDFJUJ
      ZABCDGIHUKSXLYBULZXKGXKGQZYEXKGXKGCUMUNUOUPURXICDAGBAVJZUSZUTZVAZXMXGYCXH
      CYJQYDYCXHNZCDYGGMZLVJZYHQNALVBZVAYJALBCDGIHVCYKYNYIDYNYIQYKYIYNALGYHVDVE
      VFVGVHSXIEVIMZEEWBOZVIMZYOYIEYPPOZMZDYPEPOMZYJXMMXGYOXHEFKVKZRZXGYQXHXGYO
      YOYQUUAUUAEEYPYPTVLVMRZUUBXSXKQZXIYSXGUUDXHXTRGXSQZUUDXIYSVNZHUUEUUDNXRUU
      FGXSXKVOXRUUFXGBXKMZNZAXKYHUTZYRMZVNXGYOUUGUUJUUAALBUUIEXKXKTZALXKYHVDVPS
      XRXIUUHYSUUJXRXHUUGXGGXKBVQVRXRYIUUIYRXRAGYHXKYHXRVSXRYHVTWAWCWDWEWFWGURX
      GYTXHDEFJKWHRZYIDEYPEWIWJWKXIXOAGBDWLWMZWMZYGDOUTZXMXGYCXHXOUUOQYDABDXCWM
      ZCDUUMGIHUUPTUUMTZWNSXIUUODYLYMUUNYGUSQZNZALVBZVAZXMXIYCUUNGMZUUOUVAQXGYC
      XHYDRZXGYCXHUVBYDBDUUMGHUUQWOSZALUUNUUODGUUOTHVCVMXIYOYQYOUUTYRMZYTUVAXMM
      UUBUUCUUBXIUVEYGXKMZUURNZALVBZYRMZXIYOUUNXKMUVIUUBXIUUNGXKUVDXGYFXHXGGXSX
      KHXTWPRWQALUUNUVHEXKUUKUVHTVPVMXIXRUVEUVIULYAXRUUTUVHYRXRUUSUVGALXRYLUVFU
      URGXKYGVQWRWSWCWFWTUULUUTDEYPEWIWJWKWKXDXIYOYOCXEMZXJXQULUUBUUBXIYCUVJUVC
      ABCDGIHXAWFXECEEXKXKUUKUUKXBXFWT $.
      $( [12-Jul-2012] $)
  $}


  ${
    $d A x y $.  $d B x y $.  $d G x y $.  $d J y $.  $d K x $.  $d X x y $.
    lrtrhom.1 $e |- X = ran G $.
    lrtrhom.2 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    lrtrhom.3 $e |- G = ( 1st ` K ) $.
    lrtrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a right and left translation is a
       homeomorphism.  Note that ` A ` and ` B ` are constant.  (Contributed by
       FL, 12-Jul-2012.) $)
    lrtrhom $p |- ( ( K e. TopGrp /\ A e. X /\ B e. X )
       -> F e. ( J Homeo J ) ) $=
      ( vy ctopgrp wcel w3a cv co cmpt eqid ccom chomeosm cgr topgrpgrp syl3an1
      wceq cmperltr ctop wa topgrptop 3ad2ant1 trhom 3adant2 ltrhom 3adant3 jca
      3jca cmphmp sylc eqeltrd ) GNOZBHOZCHOZPZDMHBMQERSZAHAQCERSZUAZFFUBRZVAEU
      COVBVCDVGUFEGKUDAMBCVEDEVFHJVETZVFTZIUGUEVDFUHOZVKVKPZVFVHOZVEVHOZUIVGVHO
      VAVBVLVCVAVKVKVKFGLUJZVOVOUQUKVDVMVNVAVCVMVBACVFEFGHIVJKLULUMVAVBVNVCMBVE
      EFGHIVIKLUNUOUPVFVEFFFURUSUT $.
      $( [12-Jul-2012] $)
  $}

  ${
    $d A o u v x $.  $d B o u v x $.  $d G a b o u v $.  $d X a b o u v x $.
    grphmlem0.1 $e |- H = { o | E. a e. X E. b e. X
         o = ( x e. X |-> ( ( a G x ) G b ) ) } $.
    $( Condition for a left and right translation to belong to the class of all
       the left and right translations over the class ` X ` .  (Contributed by
       FL, 16-Jul-2012.) $)
    grphmlem0 $p |- ( ( A e. X /\ B e. X /\ X e. C )
       -> ( x e. X |-> ( ( A G x ) G B ) ) e. H ) $=
      ( vu vv wcel cv co cmpt wceq wrex adantr w3a cab opreq1 opreq1d mpteq2dva
      eqid eqeq2d opreq2 rcla42ev mp3an3 3adant3 cvv wb 3ad2ant3 eqeq1 rexbidva
      mptexg elabg syl mpbird weq wa simpl rexbidv opreq2d cbvrexv syl6bb abbii
      syl6eleqr ) BHNZCHNZHDNZUAZAHBAOZFPZCFPZQZEOZAHIOZVNFPZJOZFPZQZRZJHSZIHSZ
      EUBZGVMVQVRAHLOZVNFPZMOZFPZQZRZMHSZLHSZEUBZWGVMVQWPNZVQWLRZMHSZLHSZVJVKWT
      VLVJVKVQVQRZWTVQUFWRXAVQAHVOWJFPZQZRLMBCHHWHBRZWLXCVQXDAHWKXBXDWKXBRVNHNZ
      XDWIVOWJFWHBVNFUCUDTUEUGWJCRZXCVQVQXFAHXBVPXFXBVPRXEWJCVOFUHTUEUGUIUJUKVM
      VQULNZWQWTUMVLVJXGVKAHVPDUQUNWOWTEVQULVRVQRZWNWSLHXHWNWSUMWHHNXHWMWRMHXHW
      MWRUMWJHNVRVQWLUOTUPTUPURUSUTWFWOEWEWNILHILVAZWEVRAHWIWAFPZQZRZJHSWNXIWDX
      LJHXIWCXKVRXIAHWBXJXIXEVBZVTWIWAFXMVSWHVNFXIXEVCUDUDUEUGVDXLWMJMHJMVAZXKW
      LVRXNAHXJWKXNXEVBWAWJWIFXNXEVCVEUEUGVFVGVFVHVIKVI $.
      $( [16-Jul-2012] $)
  $}

  ${
    grphm.1 $e |- X = ran G $.
    grphm.2 $e |- G = ( 1st ` K ) $.
    grphm.3 $e |- J = ( 2nd ` K ) $.
    grphm.4 $e |- H = { o | E. a e. X E. b e. X
         o = ( p e. X |-> ( ( a G p ) G b ) ) } $.

    ${
      $d G a b c d e f o p $.  $d G c d e f p q $.  $d G c d p q z $.
      $d H c d e f y $.  $d K c d e f x y $.  $d X a b c d e f o p $.
      $d X c d e f p q $.  $d o x y $.
      $( Lemma for ~ grphm .  Closure. $)
      grphmlem1 $p |- ( K e. TopGrp
       -> A. x e. H
             A. y e. H ( x o. y ) e. H ) $=
        ( wcel cv wa co wceq vc vd ve vf vq vz ctopgrp ccom cmpt wrex wi eleq2i
        cab anbi12i opreq1 opreq1d adantr mpteq2dva eqeq2d cbvrex2v abbii simpl
        weq opreq2 opreq2d bitri ax-17 visset eqeq1d 2rexbidva wb cbvmpt eqeq2i
        elabf eqeq1 w3a topgrpgrp 3ad2ant3 anim12i ancoms 3adant3 simpr cmprltr
        cgr syl111anc simp2l simp1l grpocl simp1r simp2r eqidd rcla42ev cvv crn
        eqid c1st cfv fvex eqeltri rnex mptexg ax-mp a1i elab3 sylibr syl6eleqr
        eqeltrd 3exp coeq12d eleq1d imbi2d syl5ibr sylbi com4l com12 r19.23aivv
        ex com3l imp syl2anb r19.21aivv ) GUGPZAQZBQZUHZEPZABEEYCEPZYDEPZRZYBYF
        YIYCCQZIHUAQZIQZDSZUBQZDSZUIZTZUBHUJUAHUJZCUMZPZYDYJIHUCQZYLDSZUDQZDSZU
        IZTZUDHUJUCHUJZCUMZPZRZYBYFUKZYIYCYJIHJQZYLDSZKQZDSZUIZTZKHUJJHUJZCUMZP
        ZYDUUSPZRUUJYGUUTYHUVAEUUSYCOULEUUSYDOULUNUUTYTUVAUUIUUSYSYCUURYRCUUQYQ
        YJIHYMUUNDSZUIZTJKUAUBHHJUAVCZUUPUVCYJUVDIHUUOUVBUVDUUOUVBTYLHPZUVDUUMY
        MUUNDUULYKYLDUOUPUQURUSKUBVCZUVCYPYJUVFIHUVBYOUVFUVBYOTUVEUUNYNYMDVDUQU
        RUSUTVAULUUSUUHYDUURUUGCUUQUUFYJIHUUBUUNDSZUIZTJKUCUDHHJUCVCZUUPUVHYJUV
        IIHUUOUVGUVIUVERUUMUUBUUNDUVIUUMUUBTUVEUULUUAYLDUOUQUPURUSKUDVCZUVHUUEY
        JUVJIHUVGUUDUVJUVERUUNUUCUUBDUVJUVEVBVEURUSUTVAULUNVFYTYCYPTZUBHUJUAHUJ
        ZYDUUETZUDHUJUCHUJZUUKUUIYRUVLCYCUVLCVGAVHCAVCZYQUVKUAUBHHUVOYKHPZYNHPZ
        RZRYJYCYPUVOUVRVBVIVJVNUUGUVNCYDUVNCVGBVHCBVCZUUFUVMUCUDHHUVSUUFUVMVKUU
        AHPZUUCHPZRZYJYDUUEVOUQVJVNUVLUVNUUKUVKUVNUUKUKUAUBHHUVNUVRUVKUUKUVMUVR
        UVKUUKUKUKZUCUDHHUVMUWBUWCUVKUVMUWBUVRUUKUVKYCUEHYKUEQZDSZYNDSZUIZTZUVM
        UWBUVRUUKUKZUKZUKYPUWGYCIUEUFHYOUWFUFQZYOPUEVGUWKUWFPIVGIUEVCYMUWEYNDYL
        UWDYKDVDUPVLVMUWHUVMUWJUWBUWIUWHUVMRZUVRYBUWGUUEUHZEPZUKZUKUWBUVRYBUWNU
        WBUVRYBVPZUWMIHYKUUADSZYLDSZUUCYNDSZDSZUIZEUWPDWDPZUVPUVTRZUVQUWARZUWMU
        XATYBUWBUXBUVRDGMVQVRZUWBUVRUXCYBUVRUWBUXCUVRUVPUWBUVTUVPUVQVBUVTUWAVBV
        SVTWAUWBUVRUXDYBUVRUWBUXDUVRUVQUWBUWAUVPUVQWBUVTUWAWBVSVTWAIUEYKUUAYNUU
        CUUEUWGDHUWGWOUUEWOLWCWEUWPUXAUUSEUWPUXAUUPTZKHUJJHUJZUXAUUSPUWPUWQHPZU
        WSHPZUXAUXATZUXGUWPUXBUVPUVTUXHUXEUWBUVPUVQYBWFUVTUWAUVRYBWGYKUUADHLWHW
        EUWPUXBUWAUVQUXIUXEUVTUWAUVRYBWIUWBUVPUVQYBWJUUCYNDHLWHWEUWPUXAWKUXFUXJ
        UXAIHUWRUUNDSZUIZTJKUWQUWSHHUULUWQTZUUPUXLUXAUXMIHUUOUXKUXMUUOUXKTUVEUX
        MUUMUWRUUNDUULUWQYLDUOUPUQURUSUUNUWSTZUXLUXAUXAUXNIHUXKUWTUXNUVERUUNUWS
        UWRDUXNUVEVBVEURUSWLWEUURUXGCUXAUXAWMPZUXGHWMPUXOHDWNWMLDDGWPWQWMMGWPWR
        WSWTWSIHUWTWMXAXBXCYJUXATZUUQUXFJKHHUXPUULHPUUNHPRZRYJUXAUUPUXPUXQVBVIV
        JXDXEOXFXGXHUWLUUKUWOUVRUWLYFUWNYBUWLYEUWMEUWLYCUWGYDUUEUWHUVMVBUWHUVMW
        BXIXJXKXKXLXQXMXNXOXPXRXPXSXTXMXOYA $.
        $( [17-Jul-2012] $) $( [26-Jun-2012] $)
    $}

    ${
      $d G a b c d e i p o $.  $d H g h x y z $.  $d K a b c d e f g h i p $.
      $d X a b c d e i p o $.  $d f g h o x y z $.
      $( Lemma for ~ grphm .  Associativity . $)
      grphmlem2 $p |- ( K e. TopGrp -> A. f e. H A. g e. H A. h e. H (
      ( f { <. <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x
      o. y ) ) } g ) { <. <. x , y >. , z >. | ( x e. H /\ y e. H
      /\ z = ( x o. y ) ) } h ) = ( f { <. <. x , y >. , z >. | ( x
      e. H /\ y e. H /\ z = ( x o. y ) ) } ( g { <. <. x , y >. , z
      >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) } h ) ) ) $=
        ( co vc vd ve vi ctopgrp wcel cv ccom wceq w3a copab2 wral wa cmpt wrex
        cab visset weq simpl eqeq1d 2rexbidva elab eleq2i eqeq1 rexbidv opreq1d
        wi opreq1 adantr mpteq2dva eqeq2d opreq2 cbvrex2v cgr topgrpgrp anim12i
        adantl 3jca eqid cmprltr2 syl coeq1d ad2antlr simplll simplrl syl111anc
        grpocl simplrr simpllr simprr syl112anc simprl syl122anc coeq2d grpoass
        simpr syl11anc jca eqcomd opreq12d 3eqtr4rd coeq2 coeq1 eqeq12d syl5ibr
        3eqtrd imp expdimp exp3a exp4c com14 3imp impcom com12 r19.23aivv com3l
        ex 3exp syl2anb sylbi eleq2s r19.21aiva r19.21aivva ax-17 cvv grphmlem1
        ra42 a1i df-3an oprabbii oprabval2g imp31 ralbida 2ralbida mpbird coexg
        coex opreqi syl5eq opreq2d ) KUEUFZDUGZEUGZAUGZIUFZBUGZIUFZCUGUUDUUFUHZ
        UIZUJZABCUKZTZFUGZUUKTZUUBUUCUUMUUKTZUUKTZUIZFIULZEIULDIULUUBUUCUHZUUMU
        UKTZUUBUUCUUMUHZUUKTZUIZFIULZEIULDIULZUUAUVEUUSUUMUHZUUBUVAUHZUIZFIULZE
        IULDIULUUAUVIDEIIUUAUUBIUFZUUCIUFZUMZUMZUVHFIUUMIUFZUVMUVHUVMUVHVGZUUMG
        UGZMLNUGZMUGZHTZOUGZHTZUNZUIZOLUOZNLUOZGUPZIUUMUWFUFUUMUWBUIZOLUONLUOZU
        VOUWEUWHGUUMFUQZGFURZUWCUWGNOLLUWJUVQLUFZUVTLUFZUMZUMUVPUUMUWBUWJUWMUSU
        TVAVBUVMUWHUVHUVLUUAUWHUVHVGZUVJUUBUWFUFZUUCUWFUFZUUAUWNVGZUVKIUWFUUBSV
        CIUWFUUCSVCUWOUUBUWBUIZOLUOZNLUOZUUCUWBUIZOLUOZNLUOZUWQUWPUWEUWTGUUBDUQ
        ZGDURZUWDUWSNLUXEUWCUWROLUVPUUBUWBVDVEVEVBUWEUXCGUUCEUQZGEURZUWDUXBNLUX
        GUWCUXAOLUVPUUCUWBVDVEVEVBUWTUUBMLUAUGZUVRHTZUBUGZHTZUNZUIZUBLUOUALUOZU
        UCMLUCUGZUVRHTZUDUGZHTZUNZUIZUDLUOUCLUOZUWQUXCUWRUXMUUBMLUXIUVTHTZUNZUI
        NOUAUBLLNUAURZUWBUYCUUBUYDMLUWAUYBUYDUWAUYBUIUVRLUFZUYDUVSUXIUVTHUVQUXH
        UVRHVHVFVIVJVKOUBURZUYCUXLUUBUYFMLUYBUXKUYFUYBUXKUIUYEUVTUXJUXIHVLVIVJV
        KVMUXAUXTUUCMLUXPUVTHTZUNZUINOUCUDLLNUCURZUWBUYHUUCUYIMLUWAUYGUYIUWAUYG
        UIUYEUYIUVSUXPUVTHUVQUXOUVRHVHVFVIVJVKOUDURZUYHUXSUUCUYJMLUYGUXRUYJUYGU
        XRUIUYEUVTUXQUXPHVLVIVJVKVMUXNUYAUWQUXMUYAUWQVGUAUBLLUYAUXHLUFZUXJLUFZU
        MZUXMUWQUXTUYMUXMUWQVGZVGUCUDLLUXOLUFZUXQLUFZUMZUXTUYMUYNUYQUXTUYMUJZUX
        MUUAUWNUWHUYRUXMUUAUJZUVHUWGUYSUVHVGZNOLLUWGUWMUYTUWGUWMUYSUVHUWMUYSUMU
        VHUWGUUSUWBUHZUUBUUCUWBUHZUHZUIZUYSUWMVUDUYRUXMUUAUWMVUDVGZUYQUXTUYMUXM
        UUAVUEVGZVGUXMUXTUYMUYQVUFUXMUXTUYMUYQVUFVGVGUXMUXTUMZUYMUYQUUAVUEVUGUY
        MUYQUMZUUAUMZUWMVUDUXMUXTVUIUWMUMZVUDUXTVUJUMVUDUXMUXLUUCUHZUWBUHZUXLVU
        BUHZUIZUXTVUJVUNVUJVUNUXTUXLUXSUHZUWBUHZUXLUXSUWBUHZUHZUIVUJVUPMLUXHUXO
        HTZUVRHTUXQUXJHTZHTUNZUWBUHZMLVUSUVQHTZUVRHTZUVTVUTHTZHTZUNZVURVUJVUOVV
        AUWBVUJHVNUFZUYKUYOUMZUYLUYPUMZUJZVUOVVAUIVUIVVKUWMVUIVVHVVIVVJUUAVVHVU
        HHKQVOZVQZVUHVVIUUAUYMUYKUYQUYOUYKUYLUSUYOUYPUSVPVIVUHVVJUUAUYMUYLUYQUY
        PUYKUYLWPUYOUYPWPVPVIVRVIMMUXHUXOUXJUXQUXSUXLHLUXLVSZUXSVSZPVTWAWBVUJVV
        HVUSLUFZUWKUMVUTLUFZUWLVVBVVGUIUUAVVHVUHUWMVVLWCZVUIVVPUWMUWKVUIVVHUYKU
        YOVVPVVMUYKUYLUYQUUAWDZUYMUYOUYPUUAWEZUXHUXOHLPWGWFUWKUWLUSVPVUJVVHUYPU
        YLVVQVVRVUIUYPUWMUYMUYOUYPUUAWHVIZVUIUYLUWMUYKUYLUYQUUAWIVIZUXQUXJHLPWG
        WFVUIUWKUWLWJZMMVUSUVQVUTUVTUWBVVAHLVVAVSUWBVSZPVTWKVUJUXLMLUXOUVQHTZUV
        RHTUVTUXQHTZHTUNZUHZMLUXHVWEHTZUVRHTZVWFUXJHTZHTZUNZVURVVGVUJVVHUYKVWEL
        UFZUYLVWFLUFZVWHVWMUIVVRVUIUYKUWMVVSVIZVUJVVHUYOUWKVWNVVRVUIUYOUWMVVTVI
        ZVUIUWKUWLWLZUXOUVQHLPWGWFVWBVUJVVHUWLUYPVWOVVRVWCVWAUVTUXQHLPWGWFMMUXH
        VWEUXJVWFVWGUXLHLVVNVWGVSPVTWMVUJVUQVWGUXLVUJVVHUYOUWKUYPUWLVUQVWGUIVVR
        VWQVWRVWAVWCMMUXOUVQUXQUVTUWBUXSHLVVOVWDPVTWMWNVUJMLVVFVWLVUJUYEUMZVVDV
        WJVVEVWKHVWSVVCVWIUVRHVWSVVHUYKUYOUWKUJZVVCVWIUIVUJVVHUYEVVRVIVUJVWTUYE
        VUJUYKUYOUWKVWPVWQVWRVRVIUXHUXOUVQHLPWOWQVFVWSVVHUWLUYPUYLUJZUMZVVEVWKU
        IVUJVXBUYEVUJVVHVXAVVRVUJUWLUYPUYLVWCVWAVWBVRWRVIVXBVWKVVEUVTUXQUXJHLPW
        OWSWAWTVJXAXFUXTVULVUPVUMVURUXTVUKVUOUWBUUCUXSUXLXBWBUXTVUBVUQUXLUUCUXS
        UWBXCWNXDXEXGUXMVUAVULVUCVUMUXMUUSVUKUWBUUBUXLUUCXCWBUUBUXLVUBXCXDXEXHX
        IXJXQXKXLXLXMUWGUVFVUAUVGVUCUUMUWBUUSXBUWGUVAVUBUUBUUMUWBUUCXBWNXDXEXIX
        NXOXNXRXRXOXPXOXGXSXSXSXMXNXTSYAXMYBYCUUAUVDUVIDEIIUUADYDZUUAEYDZUVMUVC
        UVHFIUVMFYDZUVMUVNUMZUUTUVFUVBUVGVXFUUSIUFZUVNUVFYEUFZUUTUVFUIUVMVXGUVN
        UUAUVLVXGUUAVXGEIULDIULUVLVXGVGDEGHIJKLMNOPQRSYFVXGDEIIYGWAXGVIUVMUVNWP
        ZVXHVXFUUSUUMUUBUUCUXDUXFYQUWIYQYHABCUUSUUMIIUUHUVFUUKUUSUUFUHYEUUDUUSU
        UFXCUUFUUMUUSXBUUJUUEUUGUMUUIUMZABCUUEUUGUUIYIYJZYKWFVXFUVJUVAIUFZUVGYE
        UFZUVBUVGUIUUAUVJUVKUVNWEZUUAUVLUVNVXLUVLUUAUVNVXLVGZUVKUUAVXOVGUVJUUAU
        VKVXOUUAUVKUVNVXLUUAVXLFIULEIULUVKUVNUMVXLVGEFGHIJKLMNOPQRSYFVXLEFIIYGW
        AXIXNVQXNYLVXMVXFUUBUVAUXDUUCUUMUXFUWIYQZYQYHABCUUBUVAIIUUHUVGUUKUUBUUF
        UHZYEUUDUUBUUFXCZUUFUVAUUBXBVXKYKWFXDYMYNYOUUAUURUVDDEIIVXCVXDUVMUUQUVC
        FIVXEVXFUUNUUTUUPUVBVXFUULUUSUUMUUKVXFUVJUVKUUSYEUFZUULUUSUIVXNUUAUVJUV
        KUVNWHZUVLVXSUUAUVNUUBUUCIIYPWCABCUUBUUCIIUUHUUSUUKVXQYEVXRUUFUUCUUBXBV
        XKYKWFVFVXFUUOUVAUUBUUKVXFUVKUVNUVAYEUFZUUOUVAUIVXTVXIVYAVXFVXPYHUVKUVN
        VYAUJUUOUUCUUMVXJABCUKZTUVAUUKVYBUUCUUMVXKYRABCUUCUUMIIUUHUVAVYBUUCUUFU
        HYEUUDUUCUUFXCUUFUUMUUCXBVYBVSYKYSWFYTXDYMYNYO $.
        $( [29-Jun-2012] $)
    $}

    ${
      $d G a b f o p u v x y z $.  $d H f x y z $.  $d K a b f p $.
      $d X a b f o p u x y z $.

      $( Lemma for ~ grphm .  Neutral element . $)
      grphlem3 $p |- ( ( K e. TopGrp /\ ( x e. X |-> ( ( ( Id ` G ) G
                         x ) G ( Id ` G ) ) ) e. H ) -> A. f e. H ( (
                         x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G )
                         ) ) { <. <. x , y >. , z >. | ( x e. H /\
                         y e. H /\ z = ( x o. y ) ) } f ) = f ) $=
        ( co wcel wceq vu vv cgi cfv cv cmpt ctopgrp ccom w3a copab2 wral ax-17
        weq opreq2 opreq1d cbvmpt eleq1i wa opreq1i cvv wrex cab topgrpgrp eqid
        wi cgr grpoidcl syl adantr a1i simpl mpteq2dva eqeq2d opreq2d syl111anc
        rcla42ev crn c1st fvex eqeltri rnex mptexg ax-mp rexbidv elab syl6eleqr
        eqeq1 sylibr simpr visset coeq1 coeq2 df-3an oprabbii oprabval2g adantl
        coex simpll simplr cmprltr2 syl122anc grpolid ex impcom adantlr grporid
        eqidd com12 imp opreq12d mpteq12dv eqeq12d imbi2d syl5ibrcom r19.23aivv
        eqtrd id sylbi eleq2s syl5eq r19.21aiv sylan2b ) AJFUCUDZAUEZFRZYCFRZUF
        ZGSIUGSZUAJYCUAUEZFRZYCFRZUFZGSZYGDUEZYDGSZBUEZGSZCUEYDYPUHZTZUIZABCUJZ
        RZYNTZDGUKYGYLGAUAUBJYFYKUBUEZYFSUAULUUDYKSZAULAUAUMYEYJYCFYDYIYCFUNUOU
        PZUQYHYMURUUCDGYHYNGSZUUCVEYMYHUUGUUCYHUUGURZUUBYLYNUUARZYNYGYLYNUUAUUF
        USUUHUUIYLYNUHZYNUUHYMUUGUUJUTSZUUIUUJTUUHYLEUEZKJLUEZKUEZFRZMUEZFRZUFZ
        TZMJVAZLJVAZEVBZGUUHYLUURTZMJVAZLJVAZYLUVBSUUHYCJSZUVFYLKJYCUUNFRZYCFRZ
        UFZTZUVEYHUVFUUGYHFVFSZUVFFIOVCZYCFJNYCVDZVGVHZVIZUVOUVJUUHUAKUBJYKUVHU
        UEKULUUDUVHSUAULUAKUMYJUVGYCFYIUUNYCFUNUOUPVJUVCUVJYLKJUVGUUPFRZUFZTLMY
        CYCJJUUMYCTZUURUVQYLUVRKJUUQUVPUVRUUNJSZURZUUOUVGUUPFUVTUUMYCUUNFUVRUVS
        VKUOUOVLVMUUPYCTZUVQUVIYLUWAKJUVPUVHUWAUVSURUUPYCUVGFUWAUVSVKVNVLVMVPVO
        UVAUVEEYLJUTSYLUTSJFVQUTNFFIVRUDUTOIVRVSVTWAVTUAJYKUTWBWCZUULYLTZUUTUVD
        LJUWCUUSUVCMJUULYLUURWGWDWDWEWHQWFYHUUGWIUUKUUHYLYNUWBDWJZWQVJABCYLYNGG
        YRUUJUUAYLYPUHUTYDYLYPWKYPYNYLWLYTYOYQURYSURABCYOYQYSWMWNWOVOUUGYHUUJYN
        TZYHUWEVEZYNUVBGYNUVBSYNUURTZMJVAZLJVAZUWFUVAUWIEYNUWDEDUMZUUTUWHLJUWJU
        USUWGMJUULYNUURWGWDWDWEUWGUWFLMJJUUMJSZUUPJSZURZUWFUWGYHYLUURUHZUURTZVE
        UWMYHUWOUWMYHURZUWNKJYCUUMFRZUUNFRZUUPYCFRZFRZUFZUURUWPUVKUVFUWKUVFUWLU
        WNUXATYHUVKUWMUVLWPYHUVFUWMUVNWPZUWKUWLYHWRUXBUWKUWLYHWSKUAYCUUMYCUUPUU
        RYLFJYLVDUURVDNWTXAUWPKJUWTJUUQUWPJXGUWPUWRUUOUWSUUPFUWKYHUWRUUOTUWLUWK
        YHURUWQUUMUUNFYHUWKUWQUUMTZYHUVKUWKUXCVEUVLUVKUWKUXCUUMYCFJNUVMXBXCVHXD
        UOXEUWMYHUWSUUPTZUWLYHUXDVEUWKYHUWLUXDYHUVKUWLUXDVEUVLUVKUWLUXDUUPYCFJN
        UVMXFXCVHXHWPXIXJXKXPXCUWGUWEUWOYHUWGUUJUWNYNUURYNUURYLWLUWGXQXLXMXNXOX
        RQXSXDXPXTXCVIYAYB $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d G o p $.  $d H h $.  $d J a b h o $.  $d K a b h $.  $d K a b p $.
      $d X b o p $.
      $( Lemma for ~ grphm .  Homeomorphisms . $)
      grphmlem4 $p |- ( K e. TopGrp -> H C_ ( J Homeo J ) ) $=
        ( vh wcel cv cima wral wi wceq ctopgrp chomeosm cuni wf1o ccnv w3a cmpt
        co wrex cab visset weq eqeq1 rexbidv elab wa crn topgrpbs cgr topgrpgrp
        eqtr eqid rltrooo 3exp syl wb f1oeq23 anidms imbi2d syl5ibr eqcoms mpan
        mpcom com3l 3imp ctop topgrptop jca 3ad2ant3 lrtrhom 3coml hmeocnb sylc
        hmeocna 3expia f1oeq1 imaeq1 ralbidv cnveq imaeq1d 3anbi123d syl5ibrcom
        3jca eleq1d r19.23aivv sylbi eleq2s com12 cvv ishomeo syl111anc sylibrd
        a1i ssrdv ) EUAOZNCDDUBUHZXENPZCOZDUCZXIXGUDZXGAPZQZDOZADRZXGUEZXKQZDOZ
        ADRZUFZXGXFOZXHXEXSXEXSSZXGXKGFHPZGPBUHIPZBUHUGZTZIFUIZHFUIZAUJZCXGYHOX
        GYDTZIFUIZHFUIZYAYGYKAXGNUKZANULZYFYJHFYMYEYIIFXKXGYDUMUNUNUOYIYAHIFFYB
        FOZYCFOZUPYAYIXEXIXIYDUDZYDXKQZDOZADRZYDUEZXKQZDOZADRZUFZSYNYOXEUUDYNYO
        XEUFZYPYSUUCYNYOXEYPXEYNYOYPBUQZXITZXEYNYOYPSZSZBDEKLURFUUFTZUUGXEUUISZ
        JUUJUUGUPFXITUUKFUUFXIVAUUKXIFXEUUIXIFTZYNYOFFYDUDZSZSZXEBUSOZUUOBEKUTU
        UPYNYOUUMGYBYCYDBFYDVBZJVCVDVEUULUUHUUNYNUULYPUUMYOUULYPUUMVFXIFXIFYDVG
        VHVIVIVJVKVEVLVMVNVOUUEDVPOZUURUPZYDXFOZYSXEYNUUSYOXEUURUURDELVQZUVAVRV
        SXEYNYOUUTGYBYCYDBDEFJUUQKLVTWAZAYDDDWBWCUUEUUSUUTUUCUUEUURUURXEYNUURYO
        UVAVSZUVCVRUVBAYDDDWDWCWMWEYIXSUUDXEYIXJYPXNYSXRUUCXIXIXGYDWFYIXMYRADYI
        XLYQDXGYDXKWGWNWHYIXQUUBADYIXPUUADYIXOYTXKXGYDWIWJWNWHWKVIWLWOWPMWQWRXE
        UURUURXGWSOZXTXSVFUVAUVAUVDXEYLXCAWSXGDDXIXIXIVBZUVEWTXAXBXD $.
        $( [12-Jul-2012] $)
    $}

    ${
      $d G a b g p u x y z $.  $d G a b o p u $.  $d G b g p u v x y z $.
      $d G p u v w x $.  $d H g u v x y z $.  $d K f u v $.  $d K u v x $.
      $d X a b g p u x y z $.  $d X a b o p u $.  $d X b g p u v x y z $.
      $d a b f g u $.  $d f o u v $.
      $( Lemma for ~ grphm .  Inverse element . $)
      grphlem5 $p |- ( K e. TopGrp -> A. f e. H E. g e. H ( g { <.
      <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) }
      f ) = ( x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G ) ) ) ) $=
        ( wcel co vu vv vw ctopgrp cv ccom wceq w3a copab2 cgi cfv cmpt wrex wi
        cab visset weq wb wa simpl eqeq1d rexbidva adantr elab mpteq2dva eqeq2d
        opreq1d opreq2d cbvrex2v cgn cvv topgrpgrp adantl simpll eqid grpoinvcl
        cgr syl11anc simplr crn rnexg syl syl5eqel grphmlem0 syl111anc cmprltr2
        oprabval2gc syl122anc opreq2 cbvmpt grpolinv ad2antlr grporinv opreq12d
        ax-17 simpllr syl5eq 3eqtrd opreq1 rcla4ev expcom com12 rexbidv syl5ibr
        imbi2d com3l mpcom ex com23 r19.23aivv sylbi eleq2s r19.21aiv ) JUDSZEU
        EZDUEZAUEZHSBUEZHSCUEXQXRUFUGUHABCUIZTZAKGUJUKZXQGTZYAGTZULZUGZEHUMZDHX
        PHSXNYFXNYFUNZXPFUEZLKMUEZLUEZGTZNUEZGTZULZUGZNKUMZMKUMZFUOZHXPYRSXPYNU
        GZNKUMZMKUMZYGYQUUAFXPDUPFDUQZYPYTMKUUBYPYTURYIKSUUBYOYSNKUUBYLKSZUSYHX
        PYNUUBUUCUTVAVBVCVBVDUUAXPLKUAUEZYJGTZUBUEZGTZULZUGZUBKUMUAKUMYGYSUUIXP
        LKUUEYLGTZULZUGMNUAUBKKMUAUQZYNUUKXPUULLKYMUUJUULYJKSZUSZYKUUEYLGUUNYIU
        UDYJGUULUUMUTVGVGVEVFNUBUQZUUKUUHXPUUOLKUUJUUGUUOUUMUSYLUUFUUEGUUOUUMUT
        VHVEVFVIUUIYGUAUBKKUUDKSZUUFKSZUSZXNUUIYFUURXNUUIYFUNZLKUUDGVJUKZUKZYJG
        TUUFUUTUKZGTULZHSZUURXNUSZUUSUVEUVAKSZUVBKSZKVKSZUVDUVEGVQSZUUPUVFXNUVI
        UURGJPVLZVMZUUPUUQXNVNZUUDGUUTKOUUTVOZVPVRZUVEUVIUUQUVGUVKUUPUUQXNVSZUU
        FGUUTKOUVMVPVRZUVEKGVTZVKOUVEUVIUVQVKSUVKGVQWAWBWCZLUVAUVBVKFGHKMNRWDWE
        ZUUIUVDUVEYFUVDUVEYFUNUUIUVEXOUUHXSTZYDUGZEHUMZUNUVEUVDUWBUVEUVCUUHXSTZ
        YDUGZUVDUWBUNUVEUWCUVCUUHUFZLKUVAUUDGTZYJGTZUUFUVBGTZGTZULZYDUVEUVDUUHH
        SZUWCUWEUGUVSUVEUUPUUQUVHUWKUVLUVOUVRLUUDUUFVKFGHKMNRWDWEABCUVCUUHHHXSX
        SVOWGVRUVEUVIUVFUUPUVGUUQUWEUWJUGUVKUVNUVLUVPUVOLLUVAUUDUVBUUFUUHUVCGKU
        VCVOUUHVOOWFWHUVEUWJAKUWFXQGTZUWHGTZULYDLAUCKUWIUWMUCUEZUWISAWOUWNUWMSL
        WOLAUQUWGUWLUWHGYJXQUWFGWIVGWJUVEAKUWMYCUVEXQKSZUSZUWLYBUWHYAGUWPUWFYAX
        QGUVEUWFYAUGZUWOUVEUVIUUPUWQUVKUVLUUDYAGUUTKOYAVOZUVMWKVRVCVGUWPUVIUUQU
        WHYAUGXNUVIUURUWOUVJWLUUPUUQXNUWOWPUUFYAGUUTKOUWRUVMWMVRWNVEWQWRUVDUWDU
        WBUWAUWDEUVCHXOUVCUGUVTUWCYDXOUVCUUHXSWSVAWTXAWBXBUUIYFUWBUVEUUIYEUWAEH
        UUIXTUVTYDXPUUHXOXSWIVAXCXEXDXFXGXHXIXJXKXKRXLXBXM $.
        $( [16-Jul-2012] $)
    $}

    ${
      $d G a a0 a1 b b0 b1 o p x y $.  $d G a b f g o p u x y z $.
      $d G a0 a1 b0 b1 p q $.  $d G a b o p u v $.  $d G p w x $.
      $d H a b f g h o u x y z $.  $d J a b o $.
      $d K a a0 a1 b b0 b1 o p x y $.  $d K a b f g h o p u x y $.
      $d X a a0 a1 b b0 b1 o p x y $.  $d X a b f g o p u x y z $.
      $d X a0 a1 b0 b1 p q $.  $d X a b o p u v $.  $d a1 b b1 o p x y z $.
      $d q z $.
      $( The functions of equation ` ( ( a G p ) G b ) ` form a group of
         homeomorphisms.  (Contributed by FL, 17-Jul-2012.) $)
      grphm $p |- ( K e. TopGrp -> ( { <. <. x , y >. , z >. |
         ( x e. H /\ y e. H /\ z = ( x o. y ) ) } e. GrpOp /\
         H C_ ( J Homeo J ) ) ) $=
        ( wcel wceq co wa vf vg vh vu vv bnjva1 bnjvb1 bnjva0 bnjvb0 vq ctopgrp
        vw cv ccom w3a copab2 cgr chomeosm wss crn cxp wf wral wrex cmpt cab wb
        wex wal eqid weq opreq1 opreq1d adantr mpteq2dva eqeq2d opreq2 rcla42ev
        wi mp3an3 cvv topgrpgrp unsgrp adantl mptexg eqeq1 rexbidv elabg mpbird
        syl cbvrexv syl6bb syl6eleqr 3adant2 c1st cfv cgi grpoidcl simpl eleq1d
        ax-mp simpr grpocl syl111anc jca sylanb eqidd mpteq12dv 3exp com3r syl6
        syl5ibr eleq2s mpcom impcom 3ad2ant3 sylib ax-17 rcla4e syl11anc df-3an
        3jca r19.23aivv com12 visset opreq12d anbi12d ex cbvexd 3bitr4g anim12i
        a17d df-rex cbvmpt a1i 3imp sylbi sylibr eqeltri raleqbidv abbii eqcomi
        eleq1i fveq2 eqcomd fveq2i grporid grpolid eqtr2d a1d com4t rneqi rnexg
        pm2.43i eqtr4i syl5eqel simp2 3adant2r simp2r r19.21aiv fopab2a crimmt1
        simp1 eqtr4d coeq2 coeq1 r2ex bicomi 2exbii eleq2i simpll simplr 3bitri
        bitri coeq12d simp2ll simp2rl simp2rr simp2lr coeq1d eqtrd biimpd com4r
        cmprltr opreq2d syl6bi com23 com3l impbid 19.21aiv eq2ab 3anbi3d cbvabv
        19.23advv 2exbidv syl6eq rnoprab grphmlem1 oprabbii grphmlem2 fvex rnex
        3eqtr4g foprab2 elab c2nd grphlem3 grphlem5 r19.26 eqeq1d eqeq2 ralbidv
        sylanbrc rcla4edv mpd mpdan xpeq12 anidms feq23 mpancom raleqdv rexeqdv
        id anbi2d rexeqbidv 3anbi123d eqcoms pm3.2i oprabex2g isgrpo grphmlem4
        ab2rexex ) HUKQZAUMZFQZBUMZFQZCUMZUYNUYPUNZRZUOZABCUPZUQQZFGGURSUSUYMVU
        CVUBUTZVUDVAZVUDVUBVBZUAUMZUBUMZVUBSUCUMZVUBSVUGVUHVUIVUBSVUBSRZUCVUDVC
        ZUBVUDVCZUAVUDVCZUDUMZVUGVUBSZVUGRZVUHVUGVUBSZVUNRZUBVUDVDZTZUAVUDVCZUD
        VUDVDZUOZFVUDRZUYMVVCUYMDUMZJIKUMZJUMZESZLUMZESZVEZRZLIVDZKIVDZDVFZVUAB
        VHAVHZCVFZFVUDUYMVVOUYOUYQVVEUYSRZUOZBVHAVHZDVFZVVQUYMVVNVVTVGZDVIVVOVW
        ARUYMVWBDUYMVVNVVTVVNUYMVVTVVLUYMVVTVSKLIIVVFIQZVVIIQZTZVVLUYMVVTVWEVVL
        UYMUOZVVRBFVDZAFVDZVVTVWFVVKFQZVVEVVKUYPUNZRZBFVDZVWHVWEUYMVWIVVLVWEUYM
        TZVVKVVOFVWMVVKVVEJIVUNVVGESZUEUMZESZVEZRZUEIVDZUDIVDZDVFZVVOVWMVVKVXAQ
        ZVVKVWQRZUEIVDZUDIVDZVWEVXEUYMVWCVWDVVKVVKRZVXEVVKVJZVXCVXFVVKJIVVHVWOE
        SZVEZRUDUEVVFVVIIIUDKVKZVWQVXIVVKVXJJIVWPVXHVXJVWPVXHRVVGIQZVXJVWNVVHVW
        OEVUNVVFVVGEVLVMVNVOVPUELVKZVXIVVKVVKVXLJIVXHVVJVXLVXHVVJRVXKVWOVVIVVHE
        VQVNVOVPVRVTVNVWMVVKWAQZVXBVXEVGVWMIWAQZVXMUYMVXNVWEUYMEUQQZVXNEHNWBZEI
        MWCWJWDJIVVJWAWEWJVWTVXEDVVKWAVVLVWSVXDUDIVVLVWRVXCUEIVVEVVKVWQWFWGWGWH
        WJWIVVNVWTDVVMVWSKUDIKUDVKZVVMVVEJIVWNVVIESZVEZRZLIVDVWSVXQVVLVXTLIVXQV
        VKVXSVVEVXQJIVVJVXRVXQVVJVXRRVXKVXQVVHVWNVVIEVVFVUNVVGEVLVMVNVOVPWGVXTV
        WRLUEILUEVKZVXSVWQVVEVYAJIVXRVWPVYAVXRVWPRVXKVVIVWOVWNEVQVNVOVPWKWLWKUU
        AWMPWMWNVWFJIVVGVEZFQVVEVVKVYBUNZRZVWLVWFVYBVVOFVWFVYBVVOQZVYBVVKRZLIVD
        ZKIVDZVWEUYMVYHVVLUYMVWEVYHUYMHWOWPZUQQZVWEVYHVSZVYIHVYIVJWBVYIWQWPZVYI
        UTZQVYJVYKVYLVYIVYMVYMVJVYLVJWRVYJVYKVSZVYLIVYMVYLIQZVYNVYJVWEVYOVYOVYH
        VYJVWEVYBJIVYLVVGESZVYLESZVEZRZVYOVYOVYHVSVSVYJVYSVWEVYJJIVVGVYQVYJVXKT
        ZVYQVYPVVGVYTVXOVYPIQZTZVYQVYPRVYJVXOVXKWUBVYIEUQEVYINUUBZUUCZVXOVXKTZV
        XOWUAVXOVXKWSZWUEVXOVYOVXKWUAWUFEVYIRZWUEVYOVSNWUEVYOWUGEWQWPZIQZVXOWUI
        VXKWUHEIMWUHVJWRZVNWUGVYLWUHIWUGWUHVYLEVYIWQUUDUUEWTXLXAVXOVXKXBVYLVVGE
        IMXCXDXEXFVYPVYLEIMVYIEWQWUCUUFZUUGWJVYJVXOVXKVYPVVGRWUDVVGVYLEIMWUKUUH
        XFUUIVOUUJVYOVYOVYSVYHVYOVYOVYSVYHVYFVYSVYBJIVYPVVIESZVEZRKLVYLVYLIIVVF
        VYLRZVVKWUMVYBWUNJIVVJIWULWUNIXGWUNVVHVYPVVIEVVFVYLVVGEVLVMXHVPVVIVYLRZ
        WUMVYRVYBWUOJIWULIVYQWUOIXGVVIVYLVYPEVQXHVPVRXIXJXKUUKUUNVYMEUTZIVYIEWU
        CUULMUUOXMXNWJXOWNVWFVYBWAQZVYEVYHVGUYMVWEWUQVVLUYMVXNWUQUYMIWUPWAMUYMV
        XOWUPWAQVXPEUQUUMWJUUPJIVVGWAWEWJXPVVNVYHDVYBWAVVEVYBRZVVMVYGKIWURVVLVY
        FLIVVEVYBVVKWFWGWGWHWJWIPWMVWFVVEVVKVYCVWEVVLUYMUUQVWFIIVVKVBZVYCVVKRVW
        EUYMWUSVVLVWMVVJIQZJIVCWUSVWMWUTJIVWMVXKVXOVVHIQZVWDUOZWUTUYMVWEVXKWVBV
        SZUYMVXOVWEWVCVSVXPVXOVWEVXKWVBVXOVWEVXKUOVXOWVAVWDVXOVWEVXKUVCVXOVWCVX
        KWVAVWDVVFVVGEIMXCUURVXOVWCVWDVXKUUSYBXIWJXOVVHVVIEIMXCXKUUTJIIVVJVVKVX
        GUVAXQWNJIIVVKUVBWJUVDVWKVYDBVYBFVYDBXRUYPVYBRVWJVYCVVEUYPVYBVVKUVEVPXS
        XTVWGVWLAVVKFVWLAXRUYNVVKRZVVRVWKBFWVDUYSVWJVVEUYNVVKUYPUVFVPWGXSXTVWHU
        YOUYQTZVVRTZBVHAVHVVTVVRABFFUVGWVFVVSABVVSWVFUYOUYQVVRYAUVHUVIUVNXQXIYC
        YDUYMVVSVVNABVVSUYMVVNUYOUYQVVRUYMVVNVSZUYQVVRWVGVSZVSZUYNVVOFUYNVVOQZW
        VDLIVDZKIVDZWVIUYNWAQWVJWVLVGAYEVVNWVLDUYNWADAVKZVVMWVKKIWVMVVLWVDLIVVE
        UYNVVKWFWGWGWHXAWVLUYNJIUFUMZVVGESZUGUMZESZVEZRZUGIVDZUFIVDZWVIWVKWVTKU
        FIVVFWVNRZVWDWVDTZLVHWVPIQZWVSTZUGVHWVKWVTWWBWWCWWELUGWWBUGXRWWBWWCUGYL
        WWBVVIWVPRZWWCWWEVGWWBWWFTZVWDWWDWVDWVSWWGVVIWVPIWWBWWFXBZWTWWGVVKWVRUY
        NWWGJIVVJWVQWWGVVJWVQRVXKWWGVVHWVOVVIWVPEWWGVVFWVNVVGEWWBWWFWSVMWWHYFVN
        VOVPYGYHYIWVDLIYMWVSUGIYMYJWKUYQWWAWVHUYQUYPJIUHUMZVVGESZUIUMZESZVEZRZU
        IIVDZUHIVDZWWAWVHVSZUYQUYPVVOQZUYPVVKRZLIVDZKIVDZWWPFVVOUYPPUVJUYPWAQWW
        RWXAVGBYEVVNWXADUYPWADBVKZVVMWWTKIWXBVVLWWSLIVVEUYPVVKWFWGWGWHXAWWTWWOK
        UHIVVFWWIRZVWDWWSTZLVHWWKIQZWWNTZUIVHWWTWWOWXCWXDWXFLUIWXCUIXRWXCWXDUIY
        LWXCVVIWWKRZWXDWXFVGWXCWXGTZVWDWXEWWSWWNWXHVVIWWKIWXCWXGXBWTWXHVVKWWMUY
        PWXHJIVVJWWLWXHVXKTZVVHWWJVVIWWKEWXIVVFWWIVVGEWXCWXGVXKUVKVMWXCWXGVXKUV
        LYFVOVPYGYHYIWWSLIYMWWNUIIYMYJWKUVMWWNWWQUHUIIIWWAWWIIQZWXETZWWNWVHWVSW
        XKWWNWVHVSZVSUFUGIIWVNIQZWWDTZWXKWVSWXLWXNWXKWVSWXLVSWVSWWNVVRWXNWXKTZW
        VGWVSWWNVVRWXOWVGVSZVSWVSWWNTZVVRVVEWVRWWMUNZRZWXPWXQUYSWXRVVEWXQUYNWVR
        UYPWWMWVSWWNWSWVSWWNXBUVOVPWXSWXOUYMVVNWXSWXOUYMUOZWVNWWIESZIQZWWKWVPES
        ZIQZVVEJIWYAVVGESZWYCESZVEZRZVVNWXTVXOWXMWXJWYBUYMWXSVXOWXOVXPXPZWXMWWD
        WXKWXSUYMUVPWXJWXEWXNWXSUYMUVQWVNWWIEIMXCXDWXTVXOWXEWWDWYDWYIWXJWXEWXNW
        XSUYMUVRWXMWWDWXKWXSUYMUVSWWKWVPEIMXCXDWXSWXOUYMWYHWXOUYMWXSWYHWXOUYMWX
        SWYHVSWXOUYMTZWXSWYHWYJWXRWYGVVEWYJVXOWXMWXJTZWWDWXETZWXRWYGRUYMVXOWXOV
        XPWDWXOWYKUYMWXNWXMWXKWXJWXMWWDWSWXJWXEWSYKVNWXOWYLUYMWXNWWDWXKWXEWXMWW
        DXBWXJWXEXBYKVNVXOWYKWYLUOZWXRUJIWVNUJUMZESZWVPESZVEZWWMUNWYGWYMWVRWYQW
        WMWVRWYQRWYMJUJCIWVQWYPUYRWVQQUJXRUYRWYPQJXRJUJVKWVOWYOWVPEVVGWYNWVNEVQ
        VMYNYOUVTJUJWVNWWIWVPWWKWWMWYQEIWYQVJWWMVJMUWDUWAXDVPUWBYHXJYPVVLWYHVVE
        JIWYEVVIESZVEZRKLWYAWYCIIVVFWYARZVVKWYSVVEWYTJIVVJWYRWYTVVJWYRRVXKWYTVV
        HWYEVVIEVVFWYAVVGEVLVMVNVOVPVVIWYCRZWYSWYGVVEXUAJIWYRWYFXUAVXKTVVIWYCWY
        EEXUAVXKWSUWEVOVPVRXDXIUWFYHUWCYHUWGYCUWHYCYQYDYQYQPXMYPYDUWNUWIUWJVVNV
        VTDUWKYRVVTVVPDCDCVKZVVSVUAABXUBVVRUYTUYOUYQVVEUYRUYSWFUWLUWOUWMUWPPVUA
        ABCUWQUXCUYMVVCVVDFFVAZFVUBVBZVUJUCFVCZUBFVCZUAFVCZVUPVURUBFVDZTZUAFVCZ
        UDFVDZUOZUYMXUDXUGXUKUYMUYSFQBFVCAFVCXUDABDEFGHIJKLMNOPUWRABCFFUYSFVUBV
        UAWVEUYTTABCUYOUYQUYTYAUWSZUXDXQABCUAUBUCDEFGHIJKLMNOPUWTUYMAIWUHUYNESZ
        WUHESZVEZFQZXUKUYMXUPVVOFUYMXUPVVKRZLIVDZKIVDZXUPVVOQUYMWUIWUIXUPJIWUHV
        VGESZWUHESZVEZRZXUTUYMVXOWUIVXPWUJWJZXVEXVDUYMAJULIXUOXVBULUMZXUOQJXRXV
        FXVBQAXRAJVKXUNXVAWUHEUYNVVGWUHEVQVMYNYOXURXVDXUPJIXVAVVIESZVEZRKLWUHWU
        HIIVVFWUHRZVVKXVHXUPXVIJIVVJIXVGXVIIXGXVIVVHXVAVVIEVVFWUHVVGEVLVMXHVPVV
        IWUHRZXVHXVCXUPXVJJIXVGIXVBXVJIXGVVIWUHXVAEVQXHVPVRXDVVNXUTDXUPVXNXUPWA
        QIWUPWAMEEVYIWANHWOUXAYSUXBYSZAIXUOWAWEXAVVEXUPRZVVMXUSKIXVLVVLXURLIVVE
        XUPVVKWFWGWGUXEYRPWMUYMXUQTZXUPVUGVUBSZVUGRZVUQXUPRZUBFVDZTZUAFVCZXUKXV
        MXVOUAFVCXVQUAFVCZXVSABCUADEFHUXFWPZHIJKLMNXWAVJPUXGUYMXVTXUQABCUAUBDEF
        GHIJKLMNOPUXHVNXVOXVQUAFUXIUXMUYMXUJXVSUDXUPFVUNXUPRZXUJXVSVGUYMXWBXUIX
        VRUAFXWBVUPXVOXUHXVQXWBVUOXVNVUGVUNXUPVUGVUBVLUXJXWBVURXVPUBFVUNXUPVUQU
        XKWGYGUXLWDUXNUXOUXPYBVVCXULVGVUDFVUDFRZVUFXUDVUMXUGVVBXUKVUEXUCRZXWCVU
        FXUDVGXWCXWDVUDFVUDFUXQUXRVUEVUDXUCFVUBUXSUXTXWCVULXUFUAVUDFXWCUYCZXWCV
        UKXUEUBVUDFXWEXWCVUJUCVUDFXWEUYAYTYTXWCVVAXUJUDVUDFXWEXWCVUTXUIUAVUDFXW
        EXWCVUSXUHVUPXWCVURUBVUDFXWEUYBUYDYTUYEUYFUYGXLXNUYMVUBWAQZVUCVVCVGUYMF
        WAQZXWGTZXWFXWHUYMXWGXWGFVVOWAPKLDIIVVKXVKXVKUYLYSZXWIUYHYOABCFFUYSWAWA
        VUBXUMUYIWJUAUBUCUDWAVUBVUDVUDVJUYJWJWIDEFGHIJKLMNOPUYKXE $.
        $( [17-Jul-2012] $)
    $}
  $}

  $c opfn $.

  $( Extend class notation with an operator that derives an operation on
     functions from an operation on the elements of the common range of those
     functions. $)
  copfn $a class opfn $.


  ${
    $d g i h x y z a b $.
    $( Multiplication or addition of two functions ` x ` and ` y ` derived from
       the operation ` g ` on the elements of the common range of ` x ` and
       ` y ` .  The functions ` x ` and ` y ` must also have the same domain
       ` i ` . $)
    df-opfn $a |- opfn = { <. <. g , i >. , h >. |
       h = { <. <. x , y >. , z >. |
         ( x e. ( dom dom g ^m i ) /\ y e. ( dom dom g ^m i ) /\
       z = { <. a , b >. | ( a e. dom x /\ b = ( ( x ` a ) g ( y ` a ) ) ) } )
        }
        } $.
  $}

