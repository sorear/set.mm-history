$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topological groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TopGrp $.

  $( Extend class notation with the class of all topological groups. $)
  ctopgrp $a class TopGrp $.

  ${
    $d g j $.
    $( Define the class of all topological groups.  A topological group is a
       group whose operation and inverse function are continuous. $)
    df-topgrp $a |- TopGrp = { <. g , j >. | ( ( g e. GrpOp /\ j e. Top ) /\
         g e. ( ( j tX j ) Cn j ) /\ ( inv ` g ) e. ( j Cn j ) ) } $.
  $}

  ${
    $d G g j $.  $d J g j $.  $d x y $.
    $( The predicate "is a topological group".  Bourbaki TG III.1 (Contributed
       by FL, 18-Apr-2010.) $)
    istopgrp $p |- ( J e. A -> ( <. G , J >. e. TopGrp <->
      ( ( G e. GrpOp /\ J e. Top ) /\ G e. ( ( J tX J ) Cn J )
        /\ ( inv ` G ) e. ( J Cn J ) ) ) ) $=
      ( vx vy vg vj wcel ctopgrp cgr ctop wa ctx co ccn cgn cfv w3a cv eleq1 id
      cop cvv wbr df-br wrel copab relopab df-topgrp releqi mpbir sylbir adantl
      brrelexi simpl jca elex adantr 3ad2ant1 eleq2i anbi1d fveq2 eleq1d anbi2d
      wceq 3anbi123d opreq12d eleq2d opelopabg syl5bb pm5.21nd ) CAHZBCUBZIHZBJ
      HZCKHZLZBCCMNZCONZHZBPQZCCONZHZRZBUCHZVLLZVLVNLWEVLVNWEVLVNBCIUDWEBCIUEBC
      IIUFDSZJHESZKHLWGWHWHMNWHONHWGPQWHWHONHRZDEUGZUFWIDEUHIWJDEUIUJUKUNULUMVL
      VNUOUPVLWDLWEVLWDWEVLVQVTWEWCVOWEVPBJUQURUSUMVLWDUOUPVNVMFSZJHZGSZKHZLZWK
      WMWMMNZWMONZHZWKPQZWMWMONZHZRZFGUGZHWFWDIXCVMFGUIUTXBVOWNLZBWQHZWAWTHZRWD
      FGBCUCAWKBVEZWOXDWRXEXAXFXGWLVOWNWKBJTVAWKBWQTXGWSWAWTWKBPVBVCVFWMCVEZXDV
      QXEVTXFWCXHWNVPVOWMCKTVDXHWQVSBXHWPVRWMCOXHWMCWMCMXHUAZXIVGXIVGVHXHWTWBWA
      XHWMCWMCOXIXIVGVHVFVIVJVK $.
      $( [18-Apr-2010] $)
  $}

  ${
    topgrpi.1 $e |- G = ( 1st ` K ) $.
    ${
      topgrpi.2 $e |- J = ( 2nd ` K ) $.
      ${
        $d G g j $.  $d J g j $.  $d K g j $.  $d N g j $.
        topgrpi.3 $e |- N = ( inv ` G ) $.
        $( "Axioms" of a topological group.  (Contributed by FL,
           18-Apr-2010.) $)
        topgrpi $p |- ( K e. TopGrp -> ( ( G e. GrpOp /\ J e. Top ) /\
          G e. ( ( J tX J ) Cn J ) /\ N e. ( J Cn J ) ) ) $=
          ( vg vj wcel cgr ctop wa ctx co ccn cgn cfv w3a wceq ctopgrp cv copab
          df-topgrp eleq2i wb eqtr3 eqcomd eleq1 anbi1d fveq2 syl6eqr 3anbi123d
          c1st eleq1d syl mpan c2nd anbi2d opreq12 anidms mpancom elopabi sylbi
          eleq2d ) CUAJCHUBZKJZIUBZLJZMZVFVHVHNOZVHPOZJZVFQRZVHVHPOZJZSZHIUCZJA
          KJZBLJZMZABBNOZBPOZJZDBBPOZJZSZUAVRCHIUDUEVQVSVIMZAVLJZDVOJZSZWGHICAC
          UNRZTZVFWLTZVQWKUFZEWMWNMZVFATZWOWPAVFAVFWLUGUHWQVJWHVMWIVPWJWQVGVSVI
          VFAKUIUJVFAVLUIWQVNDVOWQVNAQRDVFAQUKGULUOUMUPUQBCURRZTZVHWRTZWKWGUFZF
          WSWTMZVHBTZXAXBBVHBVHWRUGUHXCWHWAWIWDWJWFXCVIVTVSVHBLUIUSXCVLWCAVKWBT
          ZXCVLWCTXCXDVHBVHBNUTVAVKWBVHBPUTVBVEXCVOWEDXCVOWETVHBVHBPUTVAVEUMUPU
          QVCVD $.
          $( [18-Apr-2010] $)

        $( In a topological group, the inverse function is continuous.
           (Contributed by FL, 21-Jun-2010.) $)
        topgrpinv $p |- ( K e. TopGrp -> N e. ( J Cn J ) ) $=
          ( ctopgrp wcel cgr ctop wa ctx co ccn topgrpi simp3d ) CHIAJIBKILABBM
          NBONIDBBONIABCDEFGPQ $.
          $( [21-Jun-2010] $)
      $}

      $( In a topological group, the base set of the topology and the base set
         of the group are equal.  (Contributed by FL, 21-Jun-2010.) $)
      topgrpbs $p |- ( K e. TopGrp -> ran G = U. J ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn w3a wceq eqid wi wf fdm syl eqtr2
        cgn cfv crn cuni topgrpi wf1o grpoinvf cnf f1of cdm syl2im syl5com 3exp
        ex pm2.43i com3r imp31 3adant2 ) CFGAHGZBIGZJZABBKLBMLGZAUBUCZBBMLGZNAU
        DZBUEZOZABCVDDEVDPZUFVBVEVHVCUTVAVEVHUTVFVFVDUGZVAVEVHQQAVDVFVFPVIUHVAV
        EVJVHVAVEVJVHQZQVAVAVEVKVAVAVENVGVGVDRZVJVHVDBBVGVGVGPZVMUIVJVFVFVDRZVL
        VHQVFVFVDUJVNVDUKZVFOZVLVOVGOZVHVFVFVDSVGVGVDSVPVQVHVOVFVGUAUOULTUMUNUP
        UQTURUST $.
        $( [21-Jun-2010] $)

      $( In a topological group, the operation ` G ` is continuous.
         (Contributed by FL, 21-Jun-2010.) $)
      topgrpcn $p |- ( K e. TopGrp -> G e. ( ( J tX J ) Cn J ) ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv eqid topgrpi simp2d ) CFG
        AHGBIGJABBKLBMLGANOZBBMLGABCSDESPQR $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the operation ` G ` is a group.  (Contributed by
       FL, 21-Jun-2010.) $)
    topgrpgrp $p |- ( K e. TopGrp -> G e. GrpOp ) $=
      ( ctopgrp wcel cgr c2nd cfv ctop wa ctx co ccn eqid topgrpi simp1d simpld
      cgn ) BDEZAFEZBGHZIEZSTUBJAUAUAKLUAMLEARHZUAUAMLEAUABUCCUANUCNOPQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrptop.1 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, ` J ` is a topology.  (Contributed by FL,
       21-Jun-2010.) $)
    topgrptop $p |- ( K e. TopGrp -> J e. Top ) $=
      ( ctopgrp wcel c1st cfv cgr ctop wa ctx co ccn eqid topgrpi simp1d simprd
      cgn ) BDEZBFGZHEZAIEZSUAUBJTAAKLAMLETRGZAAMLETABUCTNCUCNOPQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    extopgrp.1 $e |- A e. _V $.
    $( The trivial topological group.  (Contributed by FL, 21-Jun-2010.)
       (Revised by Mario Carneiro, 15-Dec-2013.) $)
    extopgrp $p |- <. { <. <. A , A >. , A >. } , { (/) , { A } } >.
       e. TopGrp $=
      ( cop csn c0 cpr ctopgrp wcel cgr ctop wa ctx co ccn cvv ax-mp mp2an cuni
      eqcomi cun cgn cfv w3a wb prex istopgrp elexi grposn indistop pm3.2i eqid
      cxp wf txtop wfo crn opex rnsnop grpofo fof syl wceq 0ex snex unipr uncom
      un0 3eqtrri txuni mapudiscn cid cres ginvsn idcn eqeltri mpbir3an ) AACZA
      CDZEADZFZCGHZVRIHZVTJHZKZVRVTVTLMZVTNMHZVRUAUBZVTVTNMZHZVTOHWAWDWFWIUCUDE
      VSUEOVRVTUFPWBWCAAOBUGZUHZVSUIZUJWEJHZVSVSULZVSVRUMZWFWCWCWMWLWLVTVTWEWEU
      KZUNQWBWOWKWBWNVSVRUOWOVRVSVRUPVSVQAAAUQWJURSUSWNVSVRUTVAPWNVSVRWEWERZWNW
      CWCWQWNVBWLWLVTVTWEVSVSWPVTREVSTVSETVSEVSVCAVDZVEEVSVFVSVGVHZWSVIQSWRVJQW
      GVKVSVLZWHABVMWCWTWHHWLVTVSWSVNPVOVP $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    topgrpsubcn.1 $e |- G = ( 1st ` K ) $.
    topgrpsubcn.2 $e |- J = ( 2nd ` K ) $.
    topgrpsubcn.3 $e |- D = ( /g ` G ) $.
    ${
      $d G u x y z $.  $d J u x y $.
      topgrpsubcn.4 $e |- K e. TopGrp $.
      $( Lemma for ~ topgrpsubcn . $)
      topgrpsubcnlem $p |- D e. ( ( J tX J ) Cn J ) $=
        ( vx vy vz vu cv wcel wa co wceq eqid syl ax-mp crn cgn cfv ctx ctopgrp
        copab2 ccn cmpt2 cgr topgrpgrp grpodivfval df-mpt2 cop ccom cxp wfn wfo
        eqtri grpofo fofn 3syl simpl wi grpoinvcl adantl oprab2co w3a topgrptop
        ex ctop txtop syl2anc 3jca cid cres fvresi adantr eqcomd opeq1d pm5.32i
        oprabbii cuni topgrpbs idcn topgrpinv 2txcn syl222anc syl5eqel topgrpcn
        eqeq2d jca32 cnco eqeltri ) AIMZBUAZNZJMZWONZOZKMWNWQBUBUCZUCZBPZQOIJKU
        FZCCUDPZCUGPZAIJWOWOXBUHZXCDUENZAXFQZHXGBUINZXHBDEUJZIJABWTWOWORZWTRZGU
        KSTIJKWOWOXBULURXCBWSLMZWNXAUMZQZOZIJLUFZUNZXEXGXCXRQZHXGBWOWOUOZUPZXSX
        GXIXTWOBUQYAXJBWOXKUSXTWOBUTVAIJLKWOWOWNXAWOWOXQXCBWPWRVBWRXAWONZWPXGWR
        YBVCZHXGXIYCXJXIWRYBWQBWTWOXKXLVDVISTVEXQRXCRVFSTXDVJNZYDCVJNZVGZXQXDXD
        UGPZNZBXENZOOZXRXENXGYJHXGYFYHYIXGYDYDYEXGYEYEYDCDFVHZYKCCXDXDRZVKVLZYM
        YKVMXGXQWSXMWNVNWOVOZUCZXAUMZQZOZIJLUFZYGXPYRIJLWSXOYQWSXNYPXMWSWNYOXAW
        SYOWNWPYOWNQWRWOWNVPVQVRVSWJVTWAXGYEYEYEYEYNCCUGPZNZWTYTNYSYGNYKYKYKYKX
        GYEUUAYKCWOXGWOCWBQHBCDEFWCTZWDSBCDWTEFXLWEIJLCCXDYNWTYSCCXDWOWOYLYLUUB
        UUBYSRWFWGWHBCDEFWIWKTXQBXDXDCWLTWMWM $.
        $( [23-Dec-2013] $) $( [21-Jun-2010] $)
    $}

    $( In a topological group, the "subtraction" (or "division") is
       continuous.  Bourbaki TG III.1 axiom GT' (Contributed by FL,
       21-Jun-2010.) $)
    topgrpsubcn $p |- ( K e. TopGrp -> D e. ( ( J tX J ) Cn J ) ) $=
      ( ctopgrp wcel c1st cfv cgs ctx co ccn c2nd c0 cop csn eqid eqtri cpr cif
      fveq2i wceq fveq2 fveq2d opreq12d eleq12d 0ex elimel topgrpsubcnlem dedth
      extopgrp opreq12i syl6eleqr syl5eqel ) DHIZADJKZLKZCCMNZCONZABLKUTGBUSLEU
      DUAURUTDPKZVCMNZVCONZVBURUTVEIURDQQRQRSQQSUBRZUCZJKZLKZVGPKZVJMNZVJONZIDV
      FDVGUEZUTVIVEVLVMUSVHLDVGJUFUGVMVDVKVCVJOVMVCVJVCVJMDVGPUFZVNUHVNUHUIVIVH
      VJVGVHTVJTVITDVFHQUJUNUKULUMVAVDCVCOCVCCVCMFFUOFUOUPUQ $.
      $( [23-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    trhom.1 $e |- X = ran G $.
    trhom.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trhom.3 $e |- G = ( 1st ` K ) $.
    trhom.4 $e |- J = ( 2nd ` K ) $.
    ${
      $d A x y $.  $d G x y $.  $d K x y $.  $d X x y $.
      $( In a topological group a right translation is a homeomorphism.
         Bourbaki TG III.2 (Contributed by FL, 21-Jun-2010.) $)
      trhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
        ( vy wcel wa co wceq wi copab eqid cvv chomeosm cuni wf1o ccnv topgrpbs
        ctopgrp ccn w3a crn cgr topgrpgrp trooo ex syl eqtr f1oeq2 f1oeq3 bitrd
        eqcoms imbi2d mpan syl5ibr mpcom imp c2nd cfv cop ccom cxp wral opelxpi
        wb cv wfn ancoms ralrimiva adantl grpofo fofn 3syl adantr df-mpt df-opr
        wfo cmpt eqeq2i anbi2i opabbii 3eqtri fnopabco2b syl2anc ctop topgrptop
        txtop 3jca eqcomi unieqi eqeq12i biimpi sylan eleq2 anbi2d ax-17 anbi1d
        ctx ttcn opabbid eleq1d imbi12d mpbiri anabsi5 topgrpcn eqeltrd opreq12
        cnco syl12anc anidms eleq2d ax-mp cgs trinv grpodivfo eqtri topgrpsubcn
        wf oprex a1i rgen fopab2a mpbi c1st fvex eqeltri sylancr hmeobc syl3anc
        rnex fex mpbird ) FUFMZBGMZNZCEEUAOMZEUBZUUDCUCZCEEUGOZMZCUDZUUFMZUHZUU
        BUUEUUGUUIYTUUAUUEDUIZUUDPZYTUUAUUEQZDEFJKUEZYTUUMUULUUAGGCUCZQZYTDUJMZ
        UUPDFJUKZUUQUUAUUOABCDGIHULUMUNGUUKPZUULUUMUUPVLZHUUSUULNGUUDPZUUTGUUKU
        UDUOUVAUUEUUOUUAUUEUUOVLUUDGUUDGPUUEGUUDCUCUUOUUDGUUDCUPUUDGGCUQURUSUTU
        NVAVBVCVDEFVEVFZPZUUBUUGQKUUBUUGUVCCUVBUVBUGOZMUUBCDAVMZGMZLVMZUVEBVGZP
        ZNZALRZVHZUVDUUBUVHGGVIZMZAGVJZDUVMVNZCUVLPUUAUVOYTUUAUVNAGUVFUUAUVNUVE
        BGGVKVOVPVQZYTUVPUUAYTUUQUVMGDWDUVPUURDGHVRUVMGDVSVTWAALGUVHUVMUVKCDUVK
        SZCAGUVEBDOZWEUVFUVGUVSPZNZALRUVFUVGUVHDVFZPZNZALRIALGUVSWBUWAUWDALUVTU
        WCUVFUVSUWBUVGUVEBDWCWFWGWHWIWJWKUUBUVBWLMZUVBUVBXEOZWLMZUWEUHZUVKUVBUW
        FUGOZMZDUWFUVBUGOZMZUVLUVDMYTUWHUUAYTUWEUWGUWEUVBFUVBSZWMZYTUWEUWEUWGUW
        NUWNUVBUVBUWFUWFSWNWKUWNWOWAZYTUUAUWJYTUULGUVBUBZPZUUBUWJQZUUNUULUWQUUK
        GUUDUWPGUUKHWPEUVBKWQWRWSUWQUWRYTBUWPMZNZUVEUWPMZUVINZALRZUWIMZQYTUWEUW
        SUXDUWNALBUXCUVBUWPUWPSUXCSXFWTUWQUUBUWTUWJUXDUWQUUAUWSYTGUWPBXAXBUWQUV
        KUXCUWIUWQUVJUXBALUWQAXCUWQLXCUWQUVFUXAUVIGUWPUVEXAXDXGXHXIXJVTXKZYTUWL
        UUADUVBFJUWMXLWAUVKDUVBUWFUVBXOXPXMUVCUUFUVDCUVCUUFUVDPEUVBEUVBUGXNXQZX
        RVBXSUUBUUHAGUVEBDXTVFZOZWEZUUFYTUUQUUAUUHUXIPUURABUXGCDGIHUXGSZYAWTUVC
        UUBUXIUUFMZQKUUBUXKUVCUXIUVDMUUBUXIUXGUVKVHZUVDUUBUVOUXGUVMVNZUXIUXLPUV
        QYTUXMUUAYTUUQUVMGUXGWDUXMUURUXGDGHUXJYBUVMGUXGVSVTWAALGUVHUVMUVKUXIUXG
        UVRUXIUVFUVGUXHPZNZALRUVFUVGUVHUXGVFZPZNZALRALGUXHWBUXOUXRALUXNUXQUVFUX
        HUXPUVGUVEBUXGWCWFWGWHYCWJWKUUBUWHUWJUXGUWKMZUXLUVDMUWOUXEYTUXSUUAUXGDU
        VBFJUWMUXJYDWAUVKUXGUVBUWFUVBXOXPXMUVCUUFUVDUXIUXFXRVBXSXMWOUUBEWLMZUXT
        CTMZUUCUUJVLYTUXTUUAEFKWMWAZUYBUUBGTCYEZGTMZUYAUVSTMZAGVJUYCUYEAGUYEUVF
        UVEBDYFYGYHAGTUVSCIYIYJUYDUUBGUUKTHDDFYKVFTJFYKYLYMYQYMYGGTTCYRYNTCEEUU
        DUUDUUDSZUYFYOYPYS $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.
      $( In a topological group, a converse of a right translation is a
         homeomorphism.  Bourbaki TG III.2 (Contributed by FL, 30-Dec-2010.) $)
      cnvtrhom $p |- ( ( K e. TopGrp /\ A e. X )
        -> `' F e. ( J Homeo J ) ) $=
        ( ctopgrp wcel wa ctop chomeosm co ccnv topgrptop jca adantr trhom sylc
        cnvhmpha ) FLMZBGMZNEOMZUGNZCEEPQZMCRUIMUEUHUFUEUGUGEFKSZUJTUAABCDEFGHI
        JKUBCEEUDUC $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d F y $.  $d G x $.  $d J y $.  $d K x $.  $d O y $.
      $d X x $.
      $( In a topological group the image of an open set by a right translation
         is an open set.  Bourbaki TG III.2 (Contributed by FL,
         30-Dec-2010.) $)
      trhom2 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
       -> ( F " O ) e. J ) $=
        ( vy ctopgrp wcel cima chomeosm co wa wi trhom cv wral topgrptop adantr
        ctop jca hmeocnb syl wceq imaeq2 eleq1d rcla4cv syl6com mpcom 3impia )
        FNOZBHOZGEOZCGPZEOZCEEQROZUQURSZUSVATZABCDEFHIJKLUAVCVBCMUBZPZEOZMEUCZV
        DVCEUFOZVISZVBVHTUQVJURUQVIVIEFLUDZVKUGUEMCEEUHUIVGVAMGEVEGUJVFUTEVEGCU
        KULUMUNUOUP $.
        $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $}

    ${
      $d A x $.  $d G x $.  $d K x $.  $d X x $.  $d O x $.
      tpgprop1.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by an element is
         open .  (Contributed by FL, 30-Dec-2010.) $)
      tpgprop1 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
        -> ( O .t { A } ) e. J ) $=
        ( ctopgrp wcel w3a cpw wceq wss csn co cima topgrpgrp 3ad2ant1 simp2 wi
        cgr crn cuni topgrpbs eqtr ex pwuni pweq sseq2d mpbiri syl6 mpsyl sseld
        a1d 3imp prsubrtr2 syl3anc trhom2 eqeltrd ) FOPZBHPZGEPZQZGBUAIUBZCGUCZ
        EVJDUHPZVHGHRZPZVKVLSVGVHVMVIDFLUDUEVGVHVIUFVGVHVIVOVGVIVOUGVHVGEVNGHDU
        IZSZVGVPEUJZSZEVNTZJDEFLMUKVQVSHVRSZVTVQVSWAHVPVRULUMWAVTEVRRZTEUNWAVNW
        BEHVRUOUPUQURUSUTVAVBABGCDIHKJNVCVDABCDEFGHJKLMVEVF $.
        $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $}

    ${
      $d A y $.  $d G x y $.  $d J y $.  $d K x y $.  $d O x y $.  $d X x y $.
      tpgprop2.1 $e |- .t = ( cset ` G ) $.
      $( In a topological group, the product of an open set by another set is
         open .  (Contributed by FL, 1-Jan-2011.)  (Revised by Mario Carneiro,
         15-Dec-2013.) $)
      tpgprop2 $p |- ( ( K e. TopGrp /\ A e. ~P X /\ O e. J )
        -> ( O .t A ) e. J ) $=
        ( vy wcel cpw co wceq 3ad2ant1 ctopgrp w3a cv csn cgr cdm topgrpgrp wss
        ciun cuni pwuni a1i crn topgrpbs syl5eq pweq syl sseqtr4d ssel2 3adant2
        sylan grporndm syl5req eleqtrrd eqid iscst4 syl3anc ctop wral topgrptop
        simp2 simpl1 elelpwi expcom 3ad2ant2 imp simpl3 cmpt tpgprop1 ralrimiva
        wa wi iunopn syl2anc eqeltrd ) FUAPZBHQZPZGEPZUBZGBIRZOBGOUCZUDIRZUIZEW
        JDUEPZGDUFUFZQZPBWQPWKWNSWFWHWOWIDFLUGZTWJGWGWQWFWIGWGPZWHWFEWGUHWIWSWF
        EEUJZQZWGEXAUHWFEUKULWFHWTSWGXASWFHDUMZWTJDEFLMUNUOHWTUPUQUREWGGUSVAUTW
        JWPHSZWQWGSWFWHXCWIWFWOXCWRWOHXBWPJDVBVCUQTWPHUPUQZVDWJBWGWQWFWHWIVKXDV
        DOGBDIUEWPWPVENVFVGWJEVHPZWMEPZOBVIWNEPWFWHXEWIEFMVJTWJXFOBWJWLBPZWAWFW
        LHPZWIXFWFWHWIXGVLWJXGXHWHWFXGXHWBWIXGWHXHWLBHVMVNVOVPWFWHWIXGVQAWLAHAU
        CWLDRVRZDEFGHIJXIVELMNVSVGVTOBWMEWCWDWE $.
        $( [15-Dec-2013] $) $( [1-Jan-2011] $)
    $}

  $}

  ${
    $d A x y $.  $d G x y $.  $d J x y $.  $d X x y $.
    ltrhom.1 $e |- X = ran G $.
    ltrhom.2 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrhom.3 $e |- G = ( 1st ` K ) $.
    ltrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a left translation is a homeomorphism. ` A ` is
       constant.  (Contributed by FL, 12-Jul-2012.) $)
    ltrhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
      ( vy wcel wa co ccn wceq adantr sylan eqid ctopgrp chomeosm cuni wf1o w3a
      crn topgrpbs syl5eq cgr topgrpgrp ltrooo wb f1oeq23 anidms eqcoms syl5ibr
      ccnv mpcom cop cmpt ccom copab ltrcmp df-mpt eqcomi a1i coeq2d eqtrd ctop
      cv ctx topgrptop txtop syl2anc wi eqtr ttcn2 eleq2 anbi2d eqidd mpteq12dv
      eleq1d imbi12d mpbiri syl mpan topgrpcn cnco syl32anc eqeltrd cgn cfv cgs
      id ltrinvlem grpoinvcl syl5req eleqtrrd anbi1d opabbidv mpbird cvv ltrset
      3jca hmeobc syl3anc ) FUAMZBGMZNZCEEUBOMZEUCZXKCUDZCEEPOZMZCUQZXMMZUEZXIX
      LXNXPGXKQZXIXLXGXRXHXGGDUFZXKHDEFJKUGZUHRZXIXLXRGGCUDZXGDUIMZXHYBDFJUJZAB
      CDGIHUKSXLYBULZXKGXKGQZYEXKGXKGCUMUNUOUPURXICDAGBAVJZUSZUTZVAZXMXGYCXHCYJ
      QYDYCXHNZCDYGGMZLVJZYHQNALVBZVAYJALBCDGIHVCYKYNYIDYNYIQYKYIYNALGYHVDVEVFV
      GVHSXIEVIMZEEVKOZVIMZYOYIEYPPOZMZDYPEPOMZYJXMMXGYOXHEFKVLZRZXGYQXHXGYOYOY
      QUUAUUAEEYPYPTVMVNRZUUBXSXKQZXIYSXGUUDXHXTRGXSQZUUDXIYSVOZHUUEUUDNXRUUFGX
      SXKVPXRUUFXGBXKMZNZAXKYHUTZYRMZVOXGYOUUGUUJUUAALBUUIEXKXKTZALXKYHVDVQSXRX
      IUUHYSUUJXRXHUUGXGGXKBVRVSXRYIUUIYRXRAGYHXKYHXRWNXRYHVTWAWBWCWDWEWFURXGYT
      XHDEFJKWGRZYIDEYPEWHWIWJXIXOAGBDWKWLZWLZYGDOUTZXMXGYCXHXOUUOQYDABDWMWLZCD
      UUMGIHUUPTUUMTZWOSXIUUODYLYMUUNYGUSQZNZALVBZVAZXMXIYCUUNGMZUUOUVAQXGYCXHY
      DRZXGYCXHUVBYDBDUUMGHUUQWPSZALUUNUUODGUUOTHVCVNXIYOYQYOUUTYRMZYTUVAXMMUUB
      UUCUUBXIUVEYGXKMZUURNZALVBZYRMZXIYOUUNXKMUVIUUBXIUUNGXKUVDXGYFXHXGGXSXKHX
      TWQRWRALUUNUVHEXKUUKUVHTVQVNXIXRUVEUVIULYAXRUUTUVHYRXRUUSUVGALXRYLUVFUURG
      XKYGVRWSWTWBWEXAUULUUTDEYPEWHWIWJWJXDXIYOYOCXBMZXJXQULUUBUUBXIYCUVJUVCABC
      DGIHXCWEXBCEEXKXKUUKUUKXEXFXA $.
      $( [12-Jul-2012] $)
  $}


  ${
    $d A x y $.  $d B x y $.  $d G x y $.  $d J y $.  $d K x $.  $d X x y $.
    lrtrhom.1 $e |- X = ran G $.
    lrtrhom.2 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    lrtrhom.3 $e |- G = ( 1st ` K ) $.
    lrtrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a right and left translation is a
       homeomorphism.  Note that ` A ` and ` B ` are constant.  (Contributed by
       FL, 12-Jul-2012.) $)
    lrtrhom $p |- ( ( K e. TopGrp /\ A e. X /\ B e. X )
       -> F e. ( J Homeo J ) ) $=
      ( vy ctopgrp wcel w3a cv co cmpt eqid ccom chomeosm cgr topgrpgrp syl3an1
      wceq cmperltr ctop wa topgrptop 3ad2ant1 trhom 3adant2 ltrhom 3adant3 jca
      3jca cmphmp sylc eqeltrd ) GNOZBHOZCHOZPZDMHBMQERSZAHAQCERSZUAZFFUBRZVAEU
      COVBVCDVGUFEGKUDAMBCVEDEVFHJVETZVFTZIUGUEVDFUHOZVKVKPZVFVHOZVEVHOZUIVGVHO
      VAVBVLVCVAVKVKVKFGLUJZVOVOUQUKVDVMVNVAVCVMVBACVFEFGHIVJKLULUMVAVBVNVCMBVE
      EFGHIVIKLUNUOUPVFVEFFFURUSUT $.
      $( [12-Jul-2012] $)
  $}

  ${
    $d A o u v x $.  $d B o u v x $.  $d G a b o u v $.  $d X a b o u v x $.
    grphmlem0.1 $e |- H = { o | E. a e. X E. b e. X
         o = ( x e. X |-> ( ( a G x ) G b ) ) } $.
    $( Condition for a left and right translation to belong to the class of all
       the left and right translations over the class ` X ` .  (Contributed by
       FL, 16-Jul-2012.) $)
    grphmlem0 $p |- ( ( A e. X /\ B e. X /\ X e. C )
       -> ( x e. X |-> ( ( A G x ) G B ) ) e. H ) $=
      ( vu vv wcel cv co cmpt wceq wrex adantr w3a cab opreq1 opreq1d mpteq2dva
      eqid eqeq2d opreq2 rcla42ev mp3an3 3adant3 cvv wb 3ad2ant3 eqeq1 rexbidva
      mptexg elabg syl mpbird weq wa simpl rexbidv opreq2d cbvrexv syl6bb abbii
      syl6eleqr ) BHNZCHNZHDNZUAZAHBAOZFPZCFPZQZEOZAHIOZVNFPZJOZFPZQZRZJHSZIHSZ
      EUBZGVMVQVRAHLOZVNFPZMOZFPZQZRZMHSZLHSZEUBZWGVMVQWPNZVQWLRZMHSZLHSZVJVKWT
      VLVJVKVQVQRZWTVQUFWRXAVQAHVOWJFPZQZRLMBCHHWHBRZWLXCVQXDAHWKXBXDWKXBRVNHNZ
      XDWIVOWJFWHBVNFUCUDTUEUGWJCRZXCVQVQXFAHXBVPXFXBVPRXEWJCVOFUHTUEUGUIUJUKVM
      VQULNZWQWTUMVLVJXGVKAHVPDUQUNWOWTEVQULVRVQRZWNWSLHXHWNWSUMWHHNXHWMWRMHXHW
      MWRUMWJHNVRVQWLUOTUPTUPURUSUTWFWOEWEWNILHILVAZWEVRAHWIWAFPZQZRZJHSWNXIWDX
      LJHXIWCXKVRXIAHWBXJXIXEVBZVTWIWAFXMVSWHVNFXIXEVCUDUDUEUGVDXLWMJMHJMVAZXKW
      LVRXNAHXJWKXNXEVBWAWJWIFXNXEVCVEUEUGVFVGVFVHVIKVI $.
      $( [16-Jul-2012] $)
  $}

  ${
    grphm.1 $e |- X = ran G $.
    grphm.2 $e |- G = ( 1st ` K ) $.
    grphm.3 $e |- J = ( 2nd ` K ) $.
    grphm.4 $e |- H = { o | E. a e. X E. b e. X
         o = ( p e. X |-> ( ( a G p ) G b ) ) } $.

    ${
      $d G a b c d e f o p $.  $d G c d e f p q $.  $d G c d p q z $.
      $d H c d e f y $.  $d K c d e f x y $.  $d X a b c d e f o p $.
      $d X c d e f p q $.  $d o x y $.
      $( Lemma for ~ grpohm .  Closure. $)
      grpohmlem1 $p |- ( K e. TopGrp
       -> A. x e. H
             A. y e. H ( x o. y ) e. H ) $=
        ( wcel cv wa co wceq vc vd ve vf vq vz ctopgrp ccom cmpt wrex wi eleq2i
        cab anbi12i opreq1 opreq1d adantr mpteq2dva eqeq2d cbvrex2v abbii simpl
        weq opreq2 opreq2d bitri ax-17 vex eqeq1d 2rexbidva elabf cbvmpt eqeq2i
        wb eqeq1 w3a cgr topgrpgrp 3ad2ant3 anim12i ancoms 3adant3 eqid cmprltr
        simpr syl3anc simp2l simp1l grpocl simp1r simp2r eqidd rcla42ev cvv crn
        c1st cfv fvex eqeltri rnex mptexg ax-mp a1i elab3 sylibr syl6eleqr 3exp
        eqeltrd coeq12d eleq1d imbi2d syl5ibr sylbi com4l com12 rexlimivv com3l
        ex imp syl2anb ralrimivv ) GUGPZAQZBQZUHZEPZABEEYCEPZYDEPZRZYBYFYIYCCQZ
        IHUAQZIQZDSZUBQZDSZUIZTZUBHUJUAHUJZCUMZPZYDYJIHUCQZYLDSZUDQZDSZUIZTZUDH
        UJUCHUJZCUMZPZRZYBYFUKZYIYCYJIHJQZYLDSZKQZDSZUIZTZKHUJJHUJZCUMZPZYDUUSP
        ZRUUJYGUUTYHUVAEUUSYCOULEUUSYDOULUNUUTYTUVAUUIUUSYSYCUURYRCUUQYQYJIHYMU
        UNDSZUIZTJKUAUBHHJUAVCZUUPUVCYJUVDIHUUOUVBUVDUUOUVBTYLHPZUVDUUMYMUUNDUU
        LYKYLDUOUPUQURUSKUBVCZUVCYPYJUVFIHUVBYOUVFUVBYOTUVEUUNYNYMDVDUQURUSUTVA
        ULUUSUUHYDUURUUGCUUQUUFYJIHUUBUUNDSZUIZTJKUCUDHHJUCVCZUUPUVHYJUVIIHUUOU
        VGUVIUVERUUMUUBUUNDUVIUUMUUBTUVEUULUUAYLDUOUQUPURUSKUDVCZUVHUUEYJUVJIHU
        VGUUDUVJUVERUUNUUCUUBDUVJUVEVBVEURUSUTVAULUNVFYTYCYPTZUBHUJUAHUJZYDUUET
        ZUDHUJUCHUJZUUKUUIYRUVLCYCUVLCVGAVHCAVCZYQUVKUAUBHHUVOYKHPZYNHPZRZRYJYC
        YPUVOUVRVBVIVJVKUUGUVNCYDUVNCVGBVHCBVCZUUFUVMUCUDHHUVSUUFUVMVNUUAHPZUUC
        HPZRZYJYDUUEVOUQVJVKUVLUVNUUKUVKUVNUUKUKUAUBHHUVNUVRUVKUUKUVMUVRUVKUUKU
        KUKZUCUDHHUVMUWBUWCUVKUVMUWBUVRUUKUVKYCUEHYKUEQZDSZYNDSZUIZTZUVMUWBUVRU
        UKUKZUKZUKYPUWGYCIUEUFHYOUWFUFQZYOPUEVGUWKUWFPIVGIUEVCYMUWEYNDYLUWDYKDV
        DUPVLVMUWHUVMUWJUWBUWIUWHUVMRZUVRYBUWGUUEUHZEPZUKZUKUWBUVRYBUWNUWBUVRYB
        VPZUWMIHYKUUADSZYLDSZUUCYNDSZDSZUIZEUWPDVQPZUVPUVTRZUVQUWARZUWMUXATYBUW
        BUXBUVRDGMVRVSZUWBUVRUXCYBUVRUWBUXCUVRUVPUWBUVTUVPUVQVBUVTUWAVBVTWAWBUW
        BUVRUXDYBUVRUWBUXDUVRUVQUWBUWAUVPUVQWEUVTUWAWEVTWAWBIUEYKUUAYNUUCUUEUWG
        DHUWGWCUUEWCLWDWFUWPUXAUUSEUWPUXAUUPTZKHUJJHUJZUXAUUSPUWPUWQHPZUWSHPZUX
        AUXATZUXGUWPUXBUVPUVTUXHUXEUWBUVPUVQYBWGUVTUWAUVRYBWHYKUUADHLWIWFUWPUXB
        UWAUVQUXIUXEUVTUWAUVRYBWJUWBUVPUVQYBWKUUCYNDHLWIWFUWPUXAWLUXFUXJUXAIHUW
        RUUNDSZUIZTJKUWQUWSHHUULUWQTZUUPUXLUXAUXMIHUUOUXKUXMUUOUXKTUVEUXMUUMUWR
        UUNDUULUWQYLDUOUPUQURUSUUNUWSTZUXLUXAUXAUXNIHUXKUWTUXNUVERUUNUWSUWRDUXN
        UVEVBVEURUSWMWFUURUXGCUXAUXAWNPZUXGHWNPUXOHDWOWNLDDGWPWQWNMGWPWRWSWTWSI
        HUWTWNXAXBXCYJUXATZUUQUXFJKHHUXPUULHPUUNHPRZRYJUXAUUPUXPUXQVBVIVJXDXEOX
        FXHXGUWLUUKUWOUVRUWLYFUWNYBUWLYEUWMEUWLYCUWGYDUUEUWHUVMVBUWHUVMWEXIXJXK
        XKXLXRXMXNXOXPXQXPXSXTXMXOYA $.
        $( [17-Jul-2012] $) $( [26-Jun-2012] $)
    $}

    ${
      $d G a b c d e i p o $.  $d H g h x y z $.  $d K a b c d e f g h i p $.
      $d X a b c d e i p o $.  $d f g h o x y z $.
      $( Lemma for ~ grpohm .  Associativity . $)
      grpohmlem2 $p |- ( K e. TopGrp -> A. f e. H A. g e. H A. h e. H (
      ( f { <. <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x
      o. y ) ) } g ) { <. <. x , y >. , z >. | ( x e. H /\ y e. H
      /\ z = ( x o. y ) ) } h ) = ( f { <. <. x , y >. , z >. | ( x
      e. H /\ y e. H /\ z = ( x o. y ) ) } ( g { <. <. x , y >. , z
      >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) } h ) ) ) $=
        ( co vc vd ve vi ctopgrp wcel cv ccom wceq w3a copab2 wral wa cmpt wrex
        wi cab vex weq simpl eqeq1d 2rexbidva elab eleq2i eqeq1 rexbidv opreq1d
        opreq1 adantr mpteq2dva eqeq2d opreq2 cbvrex2v topgrpgrp adantl anim12i
        cgr simpr 3jca eqid cmprltr2 syl coeq1d ad2antlr simplll simplrl grpocl
        syl3anc simplrr simpllr simprr syl112anc simprl syl122anc coeq2d eqcomd
        grpoass syl2anc jca opreq12d 3eqtrd coeq2 coeq1 eqeq12d syl5ibr expdimp
        3eqtr4rd imp exp3a exp4c com14 3imp impcom com12 rexlimivv 3exp syl2anb
        ex com3l sylbi eleq2s ralrimiva ralrimivva cvv grpohmlem1 ra42 coex a1i
        ax-17 df-3an oprabbii imp31 ralbida 2ralbida mpbird coexg opreqi syl5eq
        oprabval2g opreq2d ) KUEUFZDUGZEUGZAUGZIUFZBUGZIUFZCUGUUDUUFUHZUIZUJZAB
        CUKZTZFUGZUUKTZUUBUUCUUMUUKTZUUKTZUIZFIULZEIULDIULUUBUUCUHZUUMUUKTZUUBU
        UCUUMUHZUUKTZUIZFIULZEIULDIULZUUAUVEUUSUUMUHZUUBUVAUHZUIZFIULZEIULDIULU
        UAUVIDEIIUUAUUBIUFZUUCIUFZUMZUMZUVHFIUUMIUFZUVMUVHUVMUVHUPZUUMGUGZMLNUG
        ZMUGZHTZOUGZHTZUNZUIZOLUOZNLUOZGUQZIUUMUWFUFUUMUWBUIZOLUONLUOZUVOUWEUWH
        GUUMFURZGFUSZUWCUWGNOLLUWJUVQLUFZUVTLUFZUMZUMUVPUUMUWBUWJUWMUTVAVBVCUVM
        UWHUVHUVLUUAUWHUVHUPZUVJUUBUWFUFZUUCUWFUFZUUAUWNUPZUVKIUWFUUBSVDIUWFUUC
        SVDUWOUUBUWBUIZOLUOZNLUOZUUCUWBUIZOLUOZNLUOZUWQUWPUWEUWTGUUBDURZGDUSZUW
        DUWSNLUXEUWCUWROLUVPUUBUWBVEVFVFVCUWEUXCGUUCEURZGEUSZUWDUXBNLUXGUWCUXAO
        LUVPUUCUWBVEVFVFVCUWTUUBMLUAUGZUVRHTZUBUGZHTZUNZUIZUBLUOUALUOZUUCMLUCUG
        ZUVRHTZUDUGZHTZUNZUIZUDLUOUCLUOZUWQUXCUWRUXMUUBMLUXIUVTHTZUNZUINOUAUBLL
        NUAUSZUWBUYCUUBUYDMLUWAUYBUYDUWAUYBUIUVRLUFZUYDUVSUXIUVTHUVQUXHUVRHVHVG
        VIVJVKOUBUSZUYCUXLUUBUYFMLUYBUXKUYFUYBUXKUIUYEUVTUXJUXIHVLVIVJVKVMUXAUX
        TUUCMLUXPUVTHTZUNZUINOUCUDLLNUCUSZUWBUYHUUCUYIMLUWAUYGUYIUWAUYGUIUYEUYI
        UVSUXPUVTHUVQUXOUVRHVHVGVIVJVKOUDUSZUYHUXSUUCUYJMLUYGUXRUYJUYGUXRUIUYEU
        VTUXQUXPHVLVIVJVKVMUXNUYAUWQUXMUYAUWQUPUAUBLLUYAUXHLUFZUXJLUFZUMZUXMUWQ
        UXTUYMUXMUWQUPZUPUCUDLLUXOLUFZUXQLUFZUMZUXTUYMUYNUYQUXTUYMUJZUXMUUAUWNU
        WHUYRUXMUUAUJZUVHUWGUYSUVHUPZNOLLUWGUWMUYTUWGUWMUYSUVHUWMUYSUMUVHUWGUUS
        UWBUHZUUBUUCUWBUHZUHZUIZUYSUWMVUDUYRUXMUUAUWMVUDUPZUYQUXTUYMUXMUUAVUEUP
        ZUPUXMUXTUYMUYQVUFUXMUXTUYMUYQVUFUPUPUXMUXTUMZUYMUYQUUAVUEVUGUYMUYQUMZU
        UAUMZUWMVUDUXMUXTVUIUWMUMZVUDUXTVUJUMVUDUXMUXLUUCUHZUWBUHZUXLVUBUHZUIZU
        XTVUJVUNVUJVUNUXTUXLUXSUHZUWBUHZUXLUXSUWBUHZUHZUIVUJVUPMLUXHUXOHTZUVRHT
        UXQUXJHTZHTUNZUWBUHZMLVUSUVQHTZUVRHTZUVTVUTHTZHTZUNZVURVUJVUOVVAUWBVUJH
        VQUFZUYKUYOUMZUYLUYPUMZUJZVUOVVAUIVUIVVKUWMVUIVVHVVIVVJUUAVVHVUHHKQVNZV
        OZVUHVVIUUAUYMUYKUYQUYOUYKUYLUTUYOUYPUTVPVIVUHVVJUUAUYMUYLUYQUYPUYKUYLV
        RUYOUYPVRVPVIVSVIMMUXHUXOUXJUXQUXSUXLHLUXLVTZUXSVTZPWAWBWCVUJVVHVUSLUFZ
        UWKUMVUTLUFZUWLVVBVVGUIUUAVVHVUHUWMVVLWDZVUIVVPUWMUWKVUIVVHUYKUYOVVPVVM
        UYKUYLUYQUUAWEZUYMUYOUYPUUAWFZUXHUXOHLPWGWHUWKUWLUTVPVUJVVHUYPUYLVVQVVR
        VUIUYPUWMUYMUYOUYPUUAWIVIZVUIUYLUWMUYKUYLUYQUUAWJVIZUXQUXJHLPWGWHVUIUWK
        UWLWKZMMVUSUVQVUTUVTUWBVVAHLVVAVTUWBVTZPWAWLVUJUXLMLUXOUVQHTZUVRHTUVTUX
        QHTZHTUNZUHZMLUXHVWEHTZUVRHTZVWFUXJHTZHTZUNZVURVVGVUJVVHUYKVWELUFZUYLVW
        FLUFZVWHVWMUIVVRVUIUYKUWMVVSVIZVUJVVHUYOUWKVWNVVRVUIUYOUWMVVTVIZVUIUWKU
        WLWMZUXOUVQHLPWGWHVWBVUJVVHUWLUYPVWOVVRVWCVWAUVTUXQHLPWGWHMMUXHVWEUXJVW
        FVWGUXLHLVVNVWGVTPWAWNVUJVUQVWGUXLVUJVVHUYOUWKUYPUWLVUQVWGUIVVRVWQVWRVW
        AVWCMMUXOUVQUXQUVTUWBUXSHLVVOVWDPWAWNWOVUJMLVVFVWLVUJUYEUMZVVDVWJVVEVWK
        HVWSVVCVWIUVRHVWSVVHUYKUYOUWKUJZVVCVWIUIVUJVVHUYEVVRVIVUJVWTUYEVUJUYKUY
        OUWKVWPVWQVWRVSVIUXHUXOUVQHLPWQWRVGVWSVVHUWLUYPUYLUJZUMZVVEVWKUIVUJVXBU
        YEVUJVVHVXAVVRVUJUWLUYPUYLVWCVWAVWBVSWSVIVXBVWKVVEUVTUXQUXJHLPWQWPWBWTV
        JXGXAUXTVULVUPVUMVURUXTVUKVUOUWBUUCUXSUXLXBWCUXTVUBVUQUXLUUCUXSUWBXCWOX
        DXEXHUXMVUAVULVUCVUMUXMUUSVUKUWBUUBUXLUUCXCWCUUBUXLVUBXCXDXEXFXIXJXRXKX
        LXLXMUWGUVFVUAUVGVUCUUMUWBUUSXBUWGUVAVUBUUBUUMUWBUUCXBWOXDXEXIXNXOXNXPX
        PXOXSXOXHXQXQXQXMXNXTSYAXMYBYCUUAUVDUVIDEIIUUADYIZUUAEYIZUVMUVCUVHFIUVM
        FYIZUVMUVNUMZUUTUVFUVBUVGVXFUUSIUFZUVNUVFYDUFZUUTUVFUIUVMVXGUVNUUAUVLVX
        GUUAVXGEIULDIULUVLVXGUPDEGHIJKLMNOPQRSYEVXGDEIIYFWBXHVIUVMUVNVRZVXHVXFU
        USUUMUUBUUCUXDUXFYGUWIYGYHABCUUSUUMIIUUHUVFUUKUUSUUFUHYDUUDUUSUUFXCUUFU
        UMUUSXBUUJUUEUUGUMUUIUMZABCUUEUUGUUIYJYKZYSWHVXFUVJUVAIUFZUVGYDUFZUVBUV
        GUIUUAUVJUVKUVNWFZUUAUVLUVNVXLUVLUUAUVNVXLUPZUVKUUAVXOUPUVJUUAUVKVXOUUA
        UVKUVNVXLUUAVXLFIULEIULUVKUVNUMVXLUPEFGHIJKLMNOPQRSYEVXLEFIIYFWBXIXNVOX
        NYLVXMVXFUUBUVAUXDUUCUUMUXFUWIYGZYGYHABCUUBUVAIIUUHUVGUUKUUBUUFUHZYDUUD
        UUBUUFXCZUUFUVAUUBXBVXKYSWHXDYMYNYOUUAUURUVDDEIIVXCVXDUVMUUQUVCFIVXEVXF
        UUNUUTUUPUVBVXFUULUUSUUMUUKVXFUVJUVKUUSYDUFZUULUUSUIVXNUUAUVJUVKUVNWIZU
        VLVXSUUAUVNUUBUUCIIYPWDABCUUBUUCIIUUHUUSUUKVXQYDVXRUUFUUCUUBXBVXKYSWHVG
        VXFUUOUVAUUBUUKVXFUVKUVNUVAYDUFZUUOUVAUIVXTVXIVYAVXFVXPYHUVKUVNVYAUJUUO
        UUCUUMVXJABCUKZTUVAUUKVYBUUCUUMVXKYQABCUUCUUMIIUUHUVAVYBUUCUUFUHYDUUDUU
        CUUFXCUUFUUMUUCXBVYBVTYSYRWHYTXDYMYNYO $.
        $( [29-Jun-2012] $)
    $}

    ${
      $d G a b f o p u v x y z $.  $d H f x y z $.  $d K a b f p $.
      $d X a b f o p u x y z $.

      $( Lemma for ~ grpohm .  Neutral element . $)
      grpohlem3 $p |- ( ( K e. TopGrp /\ ( x e. X |-> ( ( ( Id ` G ) G
                         x ) G ( Id ` G ) ) ) e. H ) -> A. f e. H ( (
                         x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G )
                         ) ) { <. <. x , y >. , z >. | ( x e. H /\
                         y e. H /\ z = ( x o. y ) ) } f ) = f ) $=
        ( co wcel wceq vu vv cgi cfv cv cmpt ctopgrp ccom w3a copab2 wral ax-17
        weq opreq2 opreq1d cbvmpt eleq1i wa opreq1i cvv wrex cab topgrpgrp eqid
        wi cgr grpoidcl syl adantr a1i simpl mpteq2dva opreq2d rcla42ev syl3anc
        eqeq2d c1st fvex eqeltri rnex mptexg ax-mp eqeq1 rexbidv elab syl6eleqr
        crn sylibr simpr vex coex coeq1 coeq2 df-3an oprabbii oprabval2g adantl
        simpll simplr cmprltr2 syl122anc eqidd grpolid ex adantlr grporid com12
        impcom imp opreq12d mpteq12dv eqtrd eqeq12d imbi2d syl5ibrcom rexlimivv
        id sylbi eleq2s syl5eq ralrimiv sylan2b ) AJFUCUDZAUEZFRZYCFRZUFZGSIUGS
        ZUAJYCUAUEZFRZYCFRZUFZGSZYGDUEZYDGSZBUEZGSZCUEYDYPUHZTZUIZABCUJZRZYNTZD
        GUKYGYLGAUAUBJYFYKUBUEZYFSUAULUUDYKSZAULAUAUMYEYJYCFYDYIYCFUNUOUPZUQYHY
        MURUUCDGYHYNGSZUUCVEYMYHUUGUUCYHUUGURZUUBYLYNUUARZYNYGYLYNUUAUUFUSUUHUU
        IYLYNUHZYNUUHYMUUGUUJUTSZUUIUUJTUUHYLEUEZKJLUEZKUEZFRZMUEZFRZUFZTZMJVAZ
        LJVAZEVBZGUUHYLUURTZMJVAZLJVAZYLUVBSUUHYCJSZUVFYLKJYCUUNFRZYCFRZUFZTZUV
        EYHUVFUUGYHFVFSZUVFFIOVCZYCFJNYCVDZVGVHZVIZUVOUVJUUHUAKUBJYKUVHUUEKULUU
        DUVHSUAULUAKUMYJUVGYCFYIUUNYCFUNUOUPVJUVCUVJYLKJUVGUUPFRZUFZTLMYCYCJJUU
        MYCTZUURUVQYLUVRKJUUQUVPUVRUUNJSZURZUUOUVGUUPFUVTUUMYCUUNFUVRUVSVKUOUOV
        LVPUUPYCTZUVQUVIYLUWAKJUVPUVHUWAUVSURUUPYCUVGFUWAUVSVKVMVLVPVNVOUVAUVEE
        YLJUTSYLUTSJFWGUTNFFIVQUDUTOIVQVRVSVTVSUAJYKUTWAWBZUULYLTZUUTUVDLJUWCUU
        SUVCMJUULYLUURWCWDWDWEWHQWFYHUUGWIUUKUUHYLYNUWBDWJZWKVJABCYLYNGGYRUUJUU
        AYLYPUHUTYDYLYPWLYPYNYLWMYTYOYQURYSURABCYOYQYSWNWOWPVOUUGYHUUJYNTZYHUWE
        VEZYNUVBGYNUVBSYNUURTZMJVAZLJVAZUWFUVAUWIEYNUWDEDUMZUUTUWHLJUWJUUSUWGMJ
        UULYNUURWCWDWDWEUWGUWFLMJJUUMJSZUUPJSZURZUWFUWGYHYLUURUHZUURTZVEUWMYHUW
        OUWMYHURZUWNKJYCUUMFRZUUNFRZUUPYCFRZFRZUFZUURUWPUVKUVFUWKUVFUWLUWNUXATY
        HUVKUWMUVLWQYHUVFUWMUVNWQZUWKUWLYHWRUXBUWKUWLYHWSKUAYCUUMYCUUPUURYLFJYL
        VDUURVDNWTXAUWPKJUWTJUUQUWPJXBUWPUWRUUOUWSUUPFUWKYHUWRUUOTUWLUWKYHURUWQ
        UUMUUNFYHUWKUWQUUMTZYHUVKUWKUXCVEUVLUVKUWKUXCUUMYCFJNUVMXCXDVHXHUOXEUWM
        YHUWSUUPTZUWLYHUXDVEUWKYHUWLUXDYHUVKUWLUXDVEUVLUVKUWLUXDUUPYCFJNUVMXFXD
        VHXGWQXIXJXKXLXDUWGUWEUWOYHUWGUUJUWNYNUURYNUURYLWMUWGXQXMXNXOXPXRQXSXHX
        LXTXDVIYAYB $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d G o p $.  $d H h $.  $d J a b h o $.  $d K a b h $.  $d K a b p $.
      $d X b o p $.
      $( Lemma for ~ grpohm .  Homeomorphisms . $)
      grpohmlem4 $p |- ( K e. TopGrp -> H C_ ( J Homeo J ) ) $=
        ( vh wcel cv cima wral wi wceq ctopgrp chomeosm cuni wf1o ccnv w3a cmpt
        co wrex cab vex weq eqeq1 rexbidv elab crn topgrpbs eqtr topgrpgrp eqid
        wa cgr rltrooo 3exp syl f1oeq23 anidms imbi2d syl5ibr eqcoms mpan mpcom
        wb com3l 3imp ctop topgrptop jca 3ad2ant3 lrtrhom 3coml hmeocnb hmeocna
        sylc 3jca 3expia f1oeq1 imaeq1 eleq1d ralbidv cnveq 3anbi123d rexlimivv
        imaeq1d syl5ibrcom sylbi eleq2s com12 cvv ishomeo syl3anc sylibrd ssrdv
        a1i ) EUAOZNCDDUBUHZXENPZCOZDUCZXIXGUDZXGAPZQZDOZADRZXGUEZXKQZDOZADRZUF
        ZXGXFOZXHXEXSXEXSSZXGXKGFHPZGPBUHIPZBUHUGZTZIFUIZHFUIZAUJZCXGYHOXGYDTZI
        FUIZHFUIZYAYGYKAXGNUKZANULZYFYJHFYMYEYIIFXKXGYDUMUNUNUOYIYAHIFFYBFOZYCF
        OZVAYAYIXEXIXIYDUDZYDXKQZDOZADRZYDUEZXKQZDOZADRZUFZSYNYOXEUUDYNYOXEUFZY
        PYSUUCYNYOXEYPXEYNYOYPBUPZXITZXEYNYOYPSZSZBDEKLUQFUUFTZUUGXEUUISZJUUJUU
        GVAFXITUUKFUUFXIURUUKXIFXEUUIXIFTZYNYOFFYDUDZSZSZXEBVBOZUUOBEKUSUUPYNYO
        UUMGYBYCYDBFYDUTZJVCVDVEUULUUHUUNYNUULYPUUMYOUULYPUUMVMXIFXIFYDVFVGVHVH
        VIVJVEVKVLVNVOUUEDVPOZUURVAZYDXFOZYSXEYNUUSYOXEUURUURDELVQZUVAVRVSXEYNY
        OUUTGYBYCYDBDEFJUUQKLVTWAZAYDDDWBWDUUEUUSUUTUUCUUEUURUURXEYNUURYOUVAVSZ
        UVCVRUVBAYDDDWCWDWEWFYIXSUUDXEYIXJYPXNYSXRUUCXIXIXGYDWGYIXMYRADYIXLYQDX
        GYDXKWHWIWJYIXQUUBADYIXPUUADYIXOYTXKXGYDWKWNWIWJWLVHWOWMWPMWQWRXEUURUUR
        XGWSOZXTXSVMUVAUVAUVDXEYLXDAWSXGDDXIXIXIUTZUVEWTXAXBXC $.
        $( [12-Jul-2012] $)
    $}

    ${
      $d G a b g p u x y z $.  $d G a b o p u $.  $d G b g p u v x y z $.
      $d G p u v w x $.  $d H g u v x y z $.  $d K f u v $.  $d K u v x $.
      $d X a b g p u x y z $.  $d X a b o p u $.  $d X b g p u v x y z $.
      $d a b f g u $.  $d f o u v $.
      $( Lemma for ~ grpohm .  Inverse element . $)
      grpohlem5 $p |- ( K e. TopGrp -> A. f e. H E. g e. H ( g { <.
      <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) }
      f ) = ( x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G ) ) ) ) $=
        ( wcel co vu vv vw ctopgrp cv ccom wceq w3a copab2 cgi cfv cmpt wrex wi
        cab vex weq wb wa simpl eqeq1d rexbidva adantr opreq1d mpteq2dva eqeq2d
        elab opreq2d cbvrex2v cgn cvv topgrpgrp adantl simpll grpoinvcl syl2anc
        cgr eqid simplr crn syl syl5eqel grphmlem0 syl3anc oprabval2gc cmprltr2
        rnexg syl122anc ax-17 opreq2 cbvmpt grpolinv ad2antlr grporinv opreq12d
        simpllr syl5eq 3eqtrd opreq1 rcla4ev expcom com12 rexbidv syl5ibr com3l
        imbi2d mpcom ex com23 rexlimivv sylbi eleq2s ralrimiv ) JUDSZEUEZDUEZAU
        EZHSBUEZHSCUEXQXRUFUGUHABCUIZTZAKGUJUKZXQGTZYAGTZULZUGZEHUMZDHXPHSXNYFX
        NYFUNZXPFUEZLKMUEZLUEZGTZNUEZGTZULZUGZNKUMZMKUMZFUOZHXPYRSXPYNUGZNKUMZM
        KUMZYGYQUUAFXPDUPFDUQZYPYTMKUUBYPYTURYIKSUUBYOYSNKUUBYLKSZUSYHXPYNUUBUU
        CUTVAVBVCVBVGUUAXPLKUAUEZYJGTZUBUEZGTZULZUGZUBKUMUAKUMYGYSUUIXPLKUUEYLG
        TZULZUGMNUAUBKKMUAUQZYNUUKXPUULLKYMUUJUULYJKSZUSZYKUUEYLGUUNYIUUDYJGUUL
        UUMUTVDVDVEVFNUBUQZUUKUUHXPUUOLKUUJUUGUUOUUMUSYLUUFUUEGUUOUUMUTVHVEVFVI
        UUIYGUAUBKKUUDKSZUUFKSZUSZXNUUIYFUURXNUUIYFUNZLKUUDGVJUKZUKZYJGTUUFUUTU
        KZGTULZHSZUURXNUSZUUSUVEUVAKSZUVBKSZKVKSZUVDUVEGVQSZUUPUVFXNUVIUURGJPVL
        ZVMZUUPUUQXNVNZUUDGUUTKOUUTVRZVOVPZUVEUVIUUQUVGUVKUUPUUQXNVSZUUFGUUTKOU
        VMVOVPZUVEKGVTZVKOUVEUVIUVQVKSUVKGVQWGWAWBZLUVAUVBVKFGHKMNRWCWDZUUIUVDU
        VEYFUVDUVEYFUNUUIUVEXOUUHXSTZYDUGZEHUMZUNUVEUVDUWBUVEUVCUUHXSTZYDUGZUVD
        UWBUNUVEUWCUVCUUHUFZLKUVAUUDGTZYJGTZUUFUVBGTZGTZULZYDUVEUVDUUHHSZUWCUWE
        UGUVSUVEUUPUUQUVHUWKUVLUVOUVRLUUDUUFVKFGHKMNRWCWDABCUVCUUHHHXSXSVRWEVPU
        VEUVIUVFUUPUVGUUQUWEUWJUGUVKUVNUVLUVPUVOLLUVAUUDUVBUUFUUHUVCGKUVCVRUUHV
        ROWFWHUVEUWJAKUWFXQGTZUWHGTZULYDLAUCKUWIUWMUCUEZUWISAWIUWNUWMSLWILAUQUW
        GUWLUWHGYJXQUWFGWJVDWKUVEAKUWMYCUVEXQKSZUSZUWLYBUWHYAGUWPUWFYAXQGUVEUWF
        YAUGZUWOUVEUVIUUPUWQUVKUVLUUDYAGUUTKOYAVRZUVMWLVPVCVDUWPUVIUUQUWHYAUGXN
        UVIUURUWOUVJWMUUPUUQXNUWOWPUUFYAGUUTKOUWRUVMWNVPWOVEWQWRUVDUWDUWBUWAUWD
        EUVCHXOUVCUGUVTUWCYDXOUVCUUHXSWSVAWTXAWAXBUUIYFUWBUVEUUIYEUWAEHUUIXTUVT
        YDXPUUHXOXSWJVAXCXFXDXEXGXHXIXJXKXKRXLXBXM $.
        $( [16-Jul-2012] $)
    $}

    ${
      $d G a a0 a1 b b0 b1 o p x y $.  $d G a b f g o p u x y z $.
      $d G a0 a1 b0 b1 p q $.  $d G a b o p u v $.  $d G p w x $.
      $d H a b f g h o u x y z $.  $d J a b o $.
      $d K a a0 a1 b b0 b1 o p x y $.  $d K a b f g h o p u x y $.
      $d X a a0 a1 b b0 b1 o p x y $.  $d X a b f g o p u x y z $.
      $d X a0 a1 b0 b1 p q $.  $d X a b o p u v $.  $d a1 b b1 o p x y z $.
      $d q z $.
      $( The functions of equation ` ( ( a G p ) G b ) ` form a group of
         homeomorphisms.  (Contributed by FL, 17-Jul-2012.) $)
      grpohm $p |- ( K e. TopGrp -> ( { <. <. x , y >. , z >. |
         ( x e. H /\ y e. H /\ z = ( x o. y ) ) } e. GrpOp /\
         H C_ ( J Homeo J ) ) ) $=
        ( wcel wceq co wa vf vg vh vu vv bnjva1 bnjvb1 bnjva0 bnjvb0 vq ctopgrp
        vw cv ccom w3a copab2 cgr chomeosm wss crn cxp wf wral wrex cmpt cab wb
        wex wal eqid weq opreq1 opreq1d adantr mpteq2dva eqeq2d opreq2 rcla42ev
        wi mp3an3 cvv topgrpgrp unsgrp adantl mptexg eqeq1 rexbidv elabg mpbird
        syl cbvrexv syl6bb syl6eleqr 3adant2 c1st cfv cgi grpoidcl simpl eleq1d
        syl5ibr ax-mp simpr grpocl syl3anc jca sylanb eqidd mpteq12dv 3exp syl6
        com3r eleq2s mpcom impcom 3ad2ant3 sylib ax-17 rcla4e syl2anc rexlimivv
        3jca df-3an com12 vex opreq12d anbi12d ex cbvexd df-rex 3bitr4g anim12i
        a17d cbvmpt a1i 3imp sylbi sylibr eqeltri raleqbidv abbii eqcomi eleq1i
        fveq2 eqcomd fveq2i grporid grpolid eqtr2d a1d com4t rneqi eqtr4i rnexg
        pm2.43i syl5eqel simp2 simp1 3adant2r simp2r fopab2a eqtr4d coeq2 coeq1
        ralrimiv crimmt1 r2ex bicomi 2exbii eleq2i simpll simplr 3bitri coeq12d
        bitri simp2ll simp2rl simp2rr simp2lr coeq1d cmprltr eqtrd biimpd com4r
        opreq2d syl6bi com23 com3l exlimdvv impbid alrimiv eq2ab 3anbi3d cbvabv
        2exbidv syl6eq rnoprab 3eqtr4g grpohmlem1 oprabbii grpohmlem2 fvex rnex
        foprab2 elab grpohlem3 grpohlem5 r19.26 sylanbrc eqeq2 ralbidv rcla4edv
        c2nd eqeq1d mpdan xpeq12 anidms feq23 mpancom id raleqdv rexeqdv anbi2d
        rexeqbidv 3anbi123d eqcoms ab2rexex pm3.2i oprabex2g isgrpo grpohmlem4
        mpd ) HUKQZAUMZFQZBUMZFQZCUMZUYNUYPUNZRZUOZABCUPZUQQZFGGURSUSUYMVUCVUBU
        TZVUDVAZVUDVUBVBZUAUMZUBUMZVUBSUCUMZVUBSVUGVUHVUIVUBSVUBSRZUCVUDVCZUBVU
        DVCZUAVUDVCZUDUMZVUGVUBSZVUGRZVUHVUGVUBSZVUNRZUBVUDVDZTZUAVUDVCZUDVUDVD
        ZUOZFVUDRZUYMVVCUYMDUMZJIKUMZJUMZESZLUMZESZVEZRZLIVDZKIVDZDVFZVUABVHAVH
        ZCVFZFVUDUYMVVOUYOUYQVVEUYSRZUOZBVHAVHZDVFZVVQUYMVVNVVTVGZDVIVVOVWARUYM
        VWBDUYMVVNVVTVVNUYMVVTVVLUYMVVTVSKLIIVVFIQZVVIIQZTZVVLUYMVVTVWEVVLUYMUO
        ZVVRBFVDZAFVDZVVTVWFVVKFQZVVEVVKUYPUNZRZBFVDZVWHVWEUYMVWIVVLVWEUYMTZVVK
        VVOFVWMVVKVVEJIVUNVVGESZUEUMZESZVEZRZUEIVDZUDIVDZDVFZVVOVWMVVKVXAQZVVKV
        WQRZUEIVDZUDIVDZVWEVXEUYMVWCVWDVVKVVKRZVXEVVKVJZVXCVXFVVKJIVVHVWOESZVEZ
        RUDUEVVFVVIIIUDKVKZVWQVXIVVKVXJJIVWPVXHVXJVWPVXHRVVGIQZVXJVWNVVHVWOEVUN
        VVFVVGEVLVMVNVOVPUELVKZVXIVVKVVKVXLJIVXHVVJVXLVXHVVJRVXKVWOVVIVVHEVQVNV
        OVPVRVTVNVWMVVKWAQZVXBVXEVGVWMIWAQZVXMUYMVXNVWEUYMEUQQZVXNEHNWBZEIMWCWJ
        WDJIVVJWAWEWJVWTVXEDVVKWAVVLVWSVXDUDIVVLVWRVXCUEIVVEVVKVWQWFWGWGWHWJWIV
        VNVWTDVVMVWSKUDIKUDVKZVVMVVEJIVWNVVIESZVEZRZLIVDVWSVXQVVLVXTLIVXQVVKVXS
        VVEVXQJIVVJVXRVXQVVJVXRRVXKVXQVVHVWNVVIEVVFVUNVVGEVLVMVNVOVPWGVXTVWRLUE
        ILUEVKZVXSVWQVVEVYAJIVXRVWPVYAVXRVWPRVXKVVIVWOVWNEVQVNVOVPWKWLWKUUAWMPW
        MWNVWFJIVVGVEZFQVVEVVKVYBUNZRZVWLVWFVYBVVOFVWFVYBVVOQZVYBVVKRZLIVDZKIVD
        ZVWEUYMVYHVVLUYMVWEVYHUYMHWOWPZUQQZVWEVYHVSZVYIHVYIVJWBVYIWQWPZVYIUTZQV
        YJVYKVYLVYIVYMVYMVJVYLVJWRVYJVYKVSZVYLIVYMVYLIQZVYNVYJVWEVYOVYOVYHVYJVW
        EVYBJIVYLVVGESZVYLESZVEZRZVYOVYOVYHVSVSVYJVYSVWEVYJJIVVGVYQVYJVXKTZVYQV
        YPVVGVYTVXOVYPIQZTZVYQVYPRVYJVXOVXKWUBVYIEUQEVYINUUBZUUCZVXOVXKTZVXOWUA
        VXOVXKWSZWUEVXOVYOVXKWUAWUFEVYIRZWUEVYOVSNWUEVYOWUGEWQWPZIQZVXOWUIVXKWU
        HEIMWUHVJWRZVNWUGVYLWUHIWUGWUHVYLEVYIWQUUDUUEWTXAXBVXOVXKXCVYLVVGEIMXDX
        EXFXGVYPVYLEIMVYIEWQWUCUUFZUUGWJVYJVXOVXKVYPVVGRWUDVVGVYLEIMWUKUUHXGUUI
        VOUUJVYOVYOVYSVYHVYOVYOVYSVYHVYFVYSVYBJIVYPVVIESZVEZRKLVYLVYLIIVVFVYLRZ
        VVKWUMVYBWUNJIVVJIWULWUNIXHWUNVVHVYPVVIEVVFVYLVVGEVLVMXIVPVVIVYLRZWUMVY
        RVYBWUOJIWULIVYQWUOIXHVVIVYLVYPEVQXIVPVRXJXLXKUUKUUOVYMEUTZIVYIEWUCUULM
        UUMXMXNWJXOWNVWFVYBWAQZVYEVYHVGUYMVWEWUQVVLUYMVXNWUQUYMIWUPWAMUYMVXOWUP
        WAQVXPEUQUUNWJUUPJIVVGWAWEWJXPVVNVYHDVYBWAVVEVYBRZVVMVYGKIWURVVLVYFLIVV
        EVYBVVKWFWGWGWHWJWIPWMVWFVVEVVKVYCVWEVVLUYMUUQVWFIIVVKVBZVYCVVKRVWEUYMW
        USVVLVWMVVJIQZJIVCWUSVWMWUTJIVWMVXKVXOVVHIQZVWDUOZWUTUYMVWEVXKWVBVSZUYM
        VXOVWEWVCVSVXPVXOVWEVXKWVBVXOVWEVXKUOVXOWVAVWDVXOVWEVXKUURVXOVWCVXKWVAV
        WDVVFVVGEIMXDUUSVXOVWCVWDVXKUUTYBXJWJXOVVHVVIEIMXDXKUVEJIIVVJVVKVXGUVAX
        QWNJIIVVKUVFWJUVBVWKVYDBVYBFVYDBXRUYPVYBRVWJVYCVVEUYPVYBVVKUVCVPXSXTVWG
        VWLAVVKFVWLAXRUYNVVKRZVVRVWKBFWVDUYSVWJVVEUYNVVKUYPUVDVPWGXSXTVWHUYOUYQ
        TZVVRTZBVHAVHVVTVVRABFFUVGWVFVVSABVVSWVFUYOUYQVVRYCUVHUVIUVOXQXJYAYDUYM
        VVSVVNABVVSUYMVVNUYOUYQVVRUYMVVNVSZUYQVVRWVGVSZVSZUYNVVOFUYNVVOQZWVDLIV
        DZKIVDZWVIUYNWAQWVJWVLVGAYEVVNWVLDUYNWADAVKZVVMWVKKIWVMVVLWVDLIVVEUYNVV
        KWFWGWGWHXBWVLUYNJIUFUMZVVGESZUGUMZESZVEZRZUGIVDZUFIVDZWVIWVKWVTKUFIVVF
        WVNRZVWDWVDTZLVHWVPIQZWVSTZUGVHWVKWVTWWBWWCWWELUGWWBUGXRWWBWWCUGYMWWBVV
        IWVPRZWWCWWEVGWWBWWFTZVWDWWDWVDWVSWWGVVIWVPIWWBWWFXCZWTWWGVVKWVRUYNWWGJ
        IVVJWVQWWGVVJWVQRVXKWWGVVHWVOVVIWVPEWWGVVFWVNVVGEWWBWWFWSVMWWHYFVNVOVPY
        GYHYIWVDLIYJWVSUGIYJYKWKUYQWWAWVHUYQUYPJIUHUMZVVGESZUIUMZESZVEZRZUIIVDZ
        UHIVDZWWAWVHVSZUYQUYPVVOQZUYPVVKRZLIVDZKIVDZWWPFVVOUYPPUVJUYPWAQWWRWXAV
        GBYEVVNWXADUYPWADBVKZVVMWWTKIWXBVVLWWSLIVVEUYPVVKWFWGWGWHXBWWTWWOKUHIVV
        FWWIRZVWDWWSTZLVHWWKIQZWWNTZUIVHWWTWWOWXCWXDWXFLUIWXCUIXRWXCWXDUIYMWXCV
        VIWWKRZWXDWXFVGWXCWXGTZVWDWXEWWSWWNWXHVVIWWKIWXCWXGXCWTWXHVVKWWMUYPWXHJ
        IVVJWWLWXHVXKTZVVHWWJVVIWWKEWXIVVFWWIVVGEWXCWXGVXKUVKVMWXCWXGVXKUVLYFVO
        VPYGYHYIWWSLIYJWWNUIIYJYKWKUVMWWNWWQUHUIIIWWAWWIIQZWXETZWWNWVHWVSWXKWWN
        WVHVSZVSUFUGIIWVNIQZWWDTZWXKWVSWXLWXNWXKWVSWXLVSWVSWWNVVRWXNWXKTZWVGWVS
        WWNVVRWXOWVGVSZVSWVSWWNTZVVRVVEWVRWWMUNZRZWXPWXQUYSWXRVVEWXQUYNWVRUYPWW
        MWVSWWNWSWVSWWNXCUVNVPWXSWXOUYMVVNWXSWXOUYMUOZWVNWWIESZIQZWWKWVPESZIQZV
        VEJIWYAVVGESZWYCESZVEZRZVVNWXTVXOWXMWXJWYBUYMWXSVXOWXOVXPXPZWXMWWDWXKWX
        SUYMUVPWXJWXEWXNWXSUYMUVQWVNWWIEIMXDXEWXTVXOWXEWWDWYDWYIWXJWXEWXNWXSUYM
        UVRWXMWWDWXKWXSUYMUVSWWKWVPEIMXDXEWXSWXOUYMWYHWXOUYMWXSWYHWXOUYMWXSWYHV
        SWXOUYMTZWXSWYHWYJWXRWYGVVEWYJVXOWXMWXJTZWWDWXETZWXRWYGRUYMVXOWXOVXPWDW
        XOWYKUYMWXNWXMWXKWXJWXMWWDWSWXJWXEWSYLVNWXOWYLUYMWXNWWDWXKWXEWXMWWDXCWX
        JWXEXCYLVNVXOWYKWYLUOZWXRUJIWVNUJUMZESZWVPESZVEZWWMUNWYGWYMWVRWYQWWMWVR
        WYQRWYMJUJCIWVQWYPUYRWVQQUJXRUYRWYPQJXRJUJVKWVOWYOWVPEVVGWYNWVNEVQVMYNY
        OUVTJUJWVNWWIWVPWWKWWMWYQEIWYQVJWWMVJMUWAUWBXEVPUWCYHXLYPVVLWYHVVEJIWYE
        VVIESZVEZRKLWYAWYCIIVVFWYARZVVKWYSVVEWYTJIVVJWYRWYTVVJWYRRVXKWYTVVHWYEV
        VIEVVFWYAVVGEVLVMVNVOVPVVIWYCRZWYSWYGVVEXUAJIWYRWYFXUAVXKTVVIWYCWYEEXUA
        VXKWSUWEVOVPVRXEXJUWFYHUWDYHUWGYAUWHYAYQYDYQYQPXMYPYDUWIUWJUWKVVNVVTDUW
        LYRVVTVVPDCDCVKZVVSVUAABXUBVVRUYTUYOUYQVVEUYRUYSWFUWMUWOUWNUWPPVUAABCUW
        QUWRUYMVVCVVDFFVAZFVUBVBZVUJUCFVCZUBFVCZUAFVCZVUPVURUBFVDZTZUAFVCZUDFVD
        ZUOZUYMXUDXUGXUKUYMUYSFQBFVCAFVCXUDABDEFGHIJKLMNOPUWSABCFFUYSFVUBVUAWVE
        UYTTABCUYOUYQUYTYCUWTZUXDXQABCUAUBUCDEFGHIJKLMNOPUXAUYMAIWUHUYNESZWUHES
        ZVEZFQZXUKUYMXUPVVOFUYMXUPVVKRZLIVDZKIVDZXUPVVOQUYMWUIWUIXUPJIWUHVVGESZ
        WUHESZVEZRZXUTUYMVXOWUIVXPWUJWJZXVEXVDUYMAJULIXUOXVBULUMZXUOQJXRXVFXVBQ
        AXRAJVKXUNXVAWUHEUYNVVGWUHEVQVMYNYOXURXVDXUPJIXVAVVIESZVEZRKLWUHWUHIIVV
        FWUHRZVVKXVHXUPXVIJIVVJIXVGXVIIXHXVIVVHXVAVVIEVVFWUHVVGEVLVMXIVPVVIWUHR
        ZXVHXVCXUPXVJJIXVGIXVBXVJIXHVVIWUHXVAEVQXIVPVRXEVVNXUTDXUPVXNXUPWAQIWUP
        WAMEEVYIWANHWOUXBYSUXCYSZAIXUOWAWEXBVVEXUPRZVVMXUSKIXVLVVLXURLIVVEXUPVV
        KWFWGWGUXEYRPWMUYMXUQTZXUPVUGVUBSZVUGRZVUQXUPRZUBFVDZTZUAFVCZXUKXVMXVOU
        AFVCXVQUAFVCZXVSABCUADEFHUXMWPZHIJKLMNXWAVJPUXFUYMXVTXUQABCUAUBDEFGHIJK
        LMNOPUXGVNXVOXVQUAFUXHUXIUYMXUJXVSUDXUPFVUNXUPRZXUJXVSVGUYMXWBXUIXVRUAF
        XWBVUPXVOXUHXVQXWBVUOXVNVUGVUNXUPVUGVUBVLUXNXWBVURXVPUBFVUNXUPVUQUXJWGY
        GUXKWDUXLUYLUXOYBVVCXULVGVUDFVUDFRZVUFXUDVUMXUGVVBXUKVUEXUCRZXWCVUFXUDV
        GXWCXWDVUDFVUDFUXPUXQVUEVUDXUCFVUBUXRUXSXWCVULXUFUAVUDFXWCUXTZXWCVUKXUE
        UBVUDFXWEXWCVUJUCVUDFXWEUYAYTYTXWCVVAXUJUDVUDFXWEXWCVUTXUIUAVUDFXWEXWCV
        USXUHVUPXWCVURUBVUDFXWEUYBUYCYTUYDUYEUYFXAXNUYMVUBWAQZVUCVVCVGUYMFWAQZX
        WGTZXWFXWHUYMXWGXWGFVVOWAPKLDIIVVKXVKXVKUYGYSZXWIUYHYOABCFFUYSWAWAVUBXU
        MUYIWJUAUBUCUDWAVUBVUDVUDVJUYJWJWIDEFGHIJKLMNOPUYKXF $.
        $( [17-Jul-2012] $)
    $}
  $}

  $c opfn $.

  $( Extend class notation with an operator that derives an operation on
     functions from an operation on the elements of the common range of those
     functions. $)
  copfn $a class opfn $.


  ${
    $d g i h x y z a b $.
    $( Multiplication or addition of two functions ` x ` and ` y ` derived from
       the operation ` g ` on the elements of the common range of ` x ` and
       ` y ` .  The functions ` x ` and ` y ` must also have the same domain
       ` i ` . $)
    df-opfn $a |- opfn = { <. <. g , i >. , h >. |
       h = { <. <. x , y >. , z >. |
         ( x e. ( dom dom g ^m i ) /\ y e. ( dom dom g ^m i ) /\
       z = { <. a , b >. | ( a e. dom x /\ b = ( ( x ` a ) g ( y ` a ) ) ) } )
        }
        } $.
  $}

