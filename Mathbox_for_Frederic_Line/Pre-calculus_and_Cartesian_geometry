$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Pre-calculus and Cartesian geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Distance between the middle of a segment and one of its extremities is a
     positive real. $)
  dmse1 $p |- ( ( A e. RR /\ B e. RR /\ A =/= B ) ->
    ( ( abs ` ( A - B ) ) / 2 ) e. RR+ ) $=
    ( cr wcel wne w3a cmin co cabs cfv crp c2 wa cdiv cc cc0 resubcl recnd
    3adant3 wceq wn wb subeq0 recn syl2an biimpd necon3ad 3impia df-ne sylibr
    absrpcl syl11anc 2re 2pos elrpii jctir rpdivcl syl ) ACDZBCDZABEZFZABGHZIJZ
    KDZLKDZMVDLNHKDVBVEVFVBVCODZVCPEZVEUSUTVGVAUSUTMZVCABQRSVBVCPTZUAZVHUSUTVAV
    KVIVJABVIVJABTZAODBODVJVLUBUSUTABUCAUDBUDUEUFUGUHVCPUIUJVCUKULLUMUNUOUPVDLU
    QUR $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( Distance between the middle of a segment and one of its extremities is a
     positive real. $)
  dmse2 $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
    ( ( abs ` ( A - B ) ) / 2 ) e. RR+ ) $=
    ( cr wcel wne cmin co cabs cfv c2 cdiv crp clt wbr dmse1 w3a ltne necomd
    syld3an3 ) ACDZBCDZABEABFGHIJKGLDABMNZABOTUAUBPBAABQRS $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( The midpoint of a segment AB of the real line is a real. $)
  msr3 $p |- ( ( A e. RR /\ B e. RR ) ->
   ( B - ( ( abs ` ( A - B ) ) / 2 ) ) e. RR ) $=
    ( cr wcel wa cmin co cabs cfv c2 cdiv simpr cc0 wne cc resubcl recnd abscl
    syl 2re a1i 2ne0 redivcl syl111anc syl11anc ) ACDZBCDZEZUGABFGZHIZJKGZCDZBU
    KFGCDUFUGLUHUJCDZJCDZJMNZULUHUIODUMUHUIABPQUIRSUNUHTUAUOUHUBUAUJJUCUDBUKPUE
    $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( The midpoint of a segment AB of the real line is a real.  (To FL:  The
     proof was shortened.  Also, it is too specialized, and set.mm size will be
     reduced if it is placed directly in the proof using it. --NM) $)
  msr4 $p |- ( ( A e. RR /\ B e. RR ) ->
    ( A + ( ( abs ` ( B - A ) ) / 2 ) ) e. RR ) $=
    ( cr wcel cmin co cabs cfv c2 cdiv caddc wa cc resubcl recnd ancoms abscl
    rehalfcl 3syl readdcl syldan ) ACDZBCDZBAEFZGHZIJFZCDZAUFKFCDUBUCLUDMDZUECD
    UGUCUBUHUCUBLUDBANOPUDQUERSAUFTUA $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( Translation and inequality on the real line. $)
  ltsubpostb $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A - B ) < A ) $=
    ( cr wcel cc0 clt wbr wa cmin co crp wi ltsubpos biimpd expcom imp32 elrp
    sylan2b ) ACDZBCDZEBFGZHABIJAFGZBKDSTUAUBTSUAUBLTSHUAUBBAMNOPBQR $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( Translation and inequality on the real line. $)
  ltaddpos2tb $p |- ( ( A e. RR /\ B e. RR+ ) -> A < ( A + B ) ) $=
    ( cr wcel cc0 clt wbr wa caddc co crp wi ltaddpos biimpd expcom imp32 elrp
    sylan2b ) ACDZBCDZEBFGZHAABIJFGZBKDSTUAUBTSUAUBLTSHUAUBBAMNOPBQR $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( The midpoint of a segment AB of the real line is on the "left" of
     ` B ` . $)
  mslb1 $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
     ( A + ( ( abs ` ( B - A ) ) / 2 ) ) < B ) $=
    ( cr wcel clt wbr w3a c2 cmul co cmin caddc cdiv cabs cfv wceq cc 2cn a1i
    recn 3ad2ant1 crp wa 3simpa anim12i ancoms abssub 3syl opreq1d dmse2
    eqeltrd rpre adddi syl111anc cc0 wne subcl abscl 2ne0 divcan2 cle resubcl
    3adant3 ltle 3impia wb pm3.22 subge0 mpbird absid syl11anc eqtrd opreq2d
    2re remulcl mpan adantr readdcl recnd rehalfcl syldan pm3.2i divmul mp3an3
    simp3 simp1 2times simpl3 jca 3ad2ant2 pncan syl breqtrrd simp2 3brtr4d
    eqbrtrd mpdan cneg negsub sub23 negid subid eqcomd anidms adantl negcl
    subadd 3eqtr2rd mulcl 3jca addass eqtr4d addcl simpr 3eqtr3d breq1d biimpa
    ltsub1 2pos ltdivmul eqbrtrrd ) ACDZBCDZABEFZGZHAIJZBAKJZLJZHMJZAYGNOZHMJZL
    JZBEYEYIYLPZHYLIJZYHPZYEYNYFHYKIJZLJZYHYEHQDZAQDZYKQDZYNYQPYRYERSZYBYCYSYDA
    TZUAZYEYKUBDYKCDZYTYEYKABKJNOZHMJUBYEYJUUEHMYEYBYCUCZBQDZYSUCZYJUUEPYBYCYDU
    DZYCYBUUHYCUUGYBYSBTZUUBUEUFZBAUGUHUIABUJUKYKULYKTUHHAYKUMUNYEYPYGYFLYEYPYJ
    YGYEYJQDZYRHUOUPZYPYJPYEYGQDZYJCDZUULYEUUFUUHUUNUUIUUKBAUQZUHYGURZYJTUHUUAU
    UMYEUSSYJHUTUNYEYGCDZUOYGVAFZYJYGPYBYCUURYDYCYBUURBAVBZUFZVCZYEUUSABVAFZYBY
    CYDUVCABVDVEYEUUFYCYBUCZUUSUVCVFUUIYBYCVGBAVHUHVIYGVJVKVLVMVLYBYCYMYOVFZYDU
    UFYHQDZYLQDZUVEUUFYHUUFYFCDZUURYHCDZYBUVHYCHCDZYBUVHVNHAVOVPZVQUVAYFYGVRZVK
    VSUUFYLYBYCUUDYLCDUUFUUNUUOUUDYCYBUUNUVDYGUUTVSUFUUQYJVTUHAYKVRWAVSUVFUVGYR
    UUMUCUVEYRUUMRUSWBYHYLHWCWDVKVCVIYEYIBEFZYHHBIJZEFZYEYFAKJZUVNBKJZEFZUVOYEY
    DUVRYBYCYDWEYEYDUCZUVPAALJZAKJZUVQEUVSYFUVTAKYEYFUVTPZYDYEYBYSUWBYBYCYDWFZU
    UBAWGUHVQUIUVSABBLJZBKJZUWAUVQEUVSABUWEEYBYCYDYDWHUVSUUGUUGUCZUWEBPYEUWFYDY
    CYBUWFYDYCUUGUUGUUJUUJWIWJVQBBWKWLWMUVSYSYSUCZUWAAPYEUWGYDYBYCUWGYDYBYSYSUU
    BUUBWIUAVQAAWKWLUVSUVNUWDBKYEUVNUWDPZYDYEYCUUGUWHYBYCYDWNZUUJBWGUHVQUIWOWPW
    QYEUVRUCZUVOYHBKJZUVQEFZYEUVRUWLYEUVPUWKUVQEYEYFAWRZLJZYHBWRZLJZUVPUWKYEUWN
    YFYGUWOLJZLJZUWPYEUWMUWQYFLYEUWQYGBKJZBBKJZAKJZUWMYEUUNUUGUWQUWSPYEUUFYSUUG
    UCZUUNUUIYBYSYCUUGUUBUUJUEZUUGYSUUNUUPUFZUHYCYBUUGYDUUJWJZYGBWSVKYEUUGUUGYS
    UXAUWSPUXEUXEUUCBBAWTUNYEUXAUWMPZAUWMLJZUWTPZYEUXGUOUWTYEYBYSUXGUOPUWCUUBAX
    AUHYCYBUOUWTPYDYCUWTUOYCUUGUWTUOPUUJBXBWLXCWJVLYEUWTQDZYSUWMQDZUXFUXHVFYEUU
    FUXBUXIUUIUXCUUGUXIYSUUGUXIBBUQXDXEUHUUCYEYBYSUXJUWCUUBAXFUHUWTAUWMXGUNVIXH
    VMYEYFQDZUUNUWOQDZGZUWPUWRPYEUUFUXBUXMUUIUXCUXBUXKUUNUXLYSUXKUUGYRYSUXKRHAX
    IVPZVQZUXDUUGUXLYSBXFXEXJUHYFYGUWOXKWLXLYEUXKYSUWNUVPPYEYBYSUXKUWCUUBUXNUHU
    UCYFAWSVKYEUVFUUGUCZUWPUWKPYEUUFUXBUXPUUIUXCUXBUVFUUGUXBUXKUUNUVFUXOUXDYFYG
    XMVKYSUUGXNWIUHYHBWSWLXOXPXQUWJUVIUVNCDZYCGZUVOUWLVFYEUXRUVRYEUVIUXQYCYEUVH
    UURUVIYBYCUVHYDUVKUAUVBUVLVKZYCYBUXQYDUVJYCUXQVNHBVOVPWJUWIXJVQYHUVNBXRWLVI
    WQYEUVIYCUVMUVOVFZUXSUWIUVIYCUVJUOHEFZUCUXTUVJUYAVNXSWBYHBHXTWDVKVIYA $.
    $( [3-Jan-2008] $) $( [2-Jan-2008] $)

  $( Two ways to state the midpoint of a segment. $)
  2wsms $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
    ( ( A + B ) / 2 ) = ( B - ( ( abs ` ( A - B ) ) / 2 ) ) ) $=
    ( cr wcel clt wbr w3a c2 caddc co cdiv cmul cmin cabs cfv wceq cc cc0 wne
    wa 3simpa recn anim12i subcl 3syl abscl 2cn a1i 2ne0 divcan2 syl111anc
    resubcl eqeltrd 3ad2ant1 3ad2ant2 addass eqcomd addcl syl11anc addcom
    simp2 2times opreq1d jca pncan cle abssuble0 ltle 3impia syld3an3 eqtr2d
    wb subadd mpbid eqtr3d 3eqtrd mulcl mpan mpbird crp dmse2 rpre subdi
    3eqtr4rd halfaddsubcl simpld msr3 mulcant2i ) ACDZBCDZABEFZGZHABIJZHKJZLJZH
    BABMJZNOZHKJZMJZLJZPZWNWSPZWLHBLJZHWRLJZMJZWMWTWOWLXEWMPZXDWMIJZXCPZWLXGXDA
    IJZBIJZBXIIJZXCWLXDQDZAQDZBQDZXGXJPWLXDWQQWLWQQDZHQDZHRSZXDWQPWLWPQDZWQCDZX
    OWLWIWJTZXMXNTZXRWIWJWKUAZWIXMWJXNAUBZBUBZUCZABUDUEWPUFZWQUBZUEXPWLUGUHZXQW
    LUIUHZWQHUJUKZWLXRXSXOWLXTWPCDXRYBABULWPUBUEYFYGUEUMZWIWJXMWKYCUNZWJWIXNWKY
    DUOZXLXMXNGXJXGXDABUPUQUKWLXIQDZXNXJXKPWLXLXMYNYKYLXDAURUSZYMXIBUTUSWLXCBMJ
    ZXIPZXKXCPZWLYPBBIJZBMJZBXIWLXCYSBMWLWJXNXCYSPWIWJWKVAZYDBVBUEVCWLWJXNXNTYT
    BPUUAWJXNXNYDYDVDBBVEUEWLAXDIJZBXIWLBAMJZXDPZUUBBPZWLXDWQUUCYJWIWJABVFFZWQU
    UCPWKABVGWIWJWKUUFABVHVIVJVKWLXNXMXLUUDUUEVLYMYLYKBAXDVMUKVNWLXMXLUUBXIPYLY
    KAXDUTUSVOVPWLXCQDZXNYNYQYRVLWLWJXNUUGUUAYDXPXNUUGUGHBVQVRUEZYMYOXCBXIVMUKV
    NVPWLUUGXLWMQDZXFXHVLUUHYKWLXTYAUUIYBYEABURUEZXCXDWMVMUKVSWLXPXNWRQDZWTXEPY
    HYMWLWRVTDWRCDUUKABWAWRWBWRUBUEHBWRWCUKWLUUIXPXQWOWMPUUJYHYIWMHUJUKWDWLWNQD
    ZWSQDZXPXAXBVLWLXTYAUULYBYEYAUULWPHKJQDABWEWFUEWLXTWSCDUUMYBABWGWSUBUEYHWNW
    SHUIWHUKVN $.
    $( [3-Jan-2008] $)

  $( The midpoint of a segment AB of the real line is on the "right" of
     ` A ` . $)
  msra3 $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
    A < ( B - ( ( abs ` ( A - B ) ) / 2 ) ) ) $=
    ( cr wcel clt wbr w3a caddc co c2 cdiv cmin cabs cfv cmul wa cc wceq
    3simpa recn adantr 2cn jctir mulcom 3syl simp1 2times eqtrd simp3 wb jca
    pncan breq1d mpbird readdcl syl11anc simp2 ltsubadd2 syl111anc mpbid
    eqbrtrd 3adant3 cc0 2re 2pos pm3.2i ltmuldiv mp3an3 2wsms breqtrd ) ACDZBCD
    ZABEFZGZAABHIZJKIZBABLIMNJKILIEVNAJOIZVOEFZAVPEFZVNVQAAHIZVOEVNVQJAOIZVTVNV
    KVLPZAQDZJQDZPVQWARVKVLVMSZWBWCWDVKWCVLATZUAUBUCAJUDUEVNVKWCWAVTRVKVLVMUFZW
    FAUGUEUHVNVTALIZBEFZVTVOEFZVNWIVMVKVLVMUIVNWBWCWCPZWIVMUJWEVKWKVLVKWCWCWFWF
    UKUAWKWHABEAAULUMUEUNVNVTCDZVKVLWIWJUJVNVKVKWLWGWGAAUOUPWGVKVLVMUQVTABURUSU
    TVAVNVKVOCDZVRVSUJZWGVKVLWMVMABUOVBVKWMJCDZVCJEFZPWNWOWPVDVEVFAVOJVGVHUPUTA
    BVIVJ $.
    $( [3-Jan-2008] $)

  ${
    $d A x $.  $d x y $.
    $( Lemma for ~ iint . $)
    iintlem1 $p |- ( ( A e. RR
                          /\ y e. |^|_ x e. RR+ ( ( A - x ) (,) ( A + x ) ) )
                       -> ( y e. RR -> y = A ) ) $=
      ( cr wcel cv crp cmin co caddc cioo ciin wceq wi wa wral eliin wn wrex
      cabs cfv c2 cdiv cc cc0 wne resubcl ancoms recnd adantr wb recn anim12i
      subeq0 syl biimpd con3d imp df-ne sylibr absrpcl syl11anc 2re 2pos
      elrpii jctir rpdivcl cxr clt wbr w3a cle wo simplr ad2antrl simpll 0re
      ltsubadd2 simp2 addid1 3syl breq2d bitrd 3exp com13 ax-mp ad2antrr
      impcom 3jca msra3 orc pm3.22 adantl msr3 leloe mpbird rexr xrlenlt mpbid
      intn3an2d jctil subge0 ltlen biimprcd com12 exp3a syl5ibr sylbid sylbird
      mslb1 simprll msr4 readdcl ex rpre syl5 ad2antlr jca adantlr intn3an3d
      pm2.61ian anim2i elioo1 mtbird jcai opreq2 opreq12d eleq2d notbid
      rcla4ev syl6 rexnal syl6ib con4d syl6bi pm2.43i com23 ) CDEZBFZAGCAFZHIZC
      YTJIZKIZLZEZYSDEZYSCMZNYRUUFUUEUUGYRUUFUUEUUGNUUEYRUUFOZUUGUUEUUHUUGNZUUE
      UUEYSUUCEZAGPZUUIAYSGUUCUUDQUUHUUKUUGUUHUUGUUKUUHUUGRZUUJRZAGSZUUKRUUHUUL
      YSCHIZTUAZUBUCIZGEZYSCUUQHIZCUUQJIZKIZEZRZOZUUNUUHUULUVDUUHUULOZUURUVCUVE
      UUPGEZUBGEZOUURUVEUVFUVGUVEUUOUDEZUUOUEUFZUVFUUHUVHUULUUHUUOUUFYRUUODEZYS
      CUGZUHUIUJUVEUUOUEMZRZUVIUUHUULUVMUUHUVLUUGUUHUVLUUGUUHYSUDEZCUDEZOZUVLUU
      GUKUUFYRUVPUUFUVNYRUVOYSULCULZUMUHYSCUNUOUPUQURUUOUEUSUTUUOVAVBUBVCVDVEVF
      UUPUBVGUOUVEUURUVCUVEUUROZUVBYSVHEZUUSYSVIVJZYSUUTVIVJZVKZUUOUEVIVJZUVRUW
      BRUWCUVROZUVTUVSUWAUWDYSUUSVLVJZUVTRZUWDUWEYSUUSVIVJZYSUUSMZVMZUWDUUFYRYS
      CVIVJZVKUWGUWIUWDUUFYRUWJUVEUUFUWCUURYRUUFUULVNZVOZUVEYRUWCUURYRUUFUULVPZ
      VOUVRUWCUWJUUHUWCUWJNZUULUURYRUUFUWNUEDEZYRUUFUWNNNVQUUFYRUWOUWNUUFYRUWOU
      WNUUFYRUWOVKZUWCUWJUWPUWCYSCUEJIZVIVJUWJYSCUEVRUWPUWQCYSVIUWPYRUVOUWQCMUU
      FYRUWOVSUVQCVTWAWBWCUPWDWEWFURWGWHWIYSCWJUWGUWHWKWAUWDUUFUUSDEZUWEUWIUKUW
      LUWDUUFYROZUWRUVRUWSUWCUUHUWSUULUURYRUUFWLWGZWMYSCWNZUOYSUUSWOVBWPUWDUVSU
      USVHEZUWEUWFUKUVRUVSUWCUUHUVSUULUURUUFUVSYRYSWQZWMWGWMUVRUXBUWCUVRUWSUWRU
      XBUWTUXAUUSWQZWAWMYSUUSWRVBWSWTUWCRZUVROZUWAUVSUVTUXFUUTYSVLVJZUWARZUXFUX
      GUUTYSVIVJZUUTYSMZVMZUXFYRUUFCYSVIVJZVKUXIUXKUXFYRUUFUXLUVEYRUXEUURUWMVOU
      VEUUFUXEUURUWKVOZUVRUXEUXLUVEUXEUXLNUURUVEUXEUEUUOVLVJZUXLUVEUEVHEZUUOVHE
      ZOUXNUXEUKUVEUXPUXOUUHUXPUULUUFYRUXPUWSUVJUXPUVKUUOWQUOUHUJUWOUXOVQUEWQWF
      XAUEUUOWRUOUVEUXNCYSVLVJZUXLUUHUXNUXQUKZUULUUFYRUXRYSCXBUHUJUUHUULUXQUXLN
      ZUUHYSCUFZUXSUULUUHUXTUXQUXLUXTUXQOUUHUXLUXQUXTUUHUXLNUUHUXLUXQUXTOCYSXCX
      DUHXEXFYSCUSXGURXHXIUJWHWICYSXJUXIUXJWKWAUXFUUTDEZUUFUXGUXKUKUXFUUHUYAUXE
      UUHUULUURXKCYSXLUOUXMUUTYSWOVBWPUXFUUTVHEZUVSOZUXGUXHUKUVRUYCUXEUUHUURUYC
      UULUUHUUROUYBUVSUUHUURUYBYRUURUYBNUUFYRUUQDEZUYBUURYRUYDUYBYRUYDOZUYAUYBC
      UUQXMZUUTWQZUOXNUUQXOZXPUJURUUFUVSYRUURUXCXQXRXSWMUUTYSWRUOWSXTYAUVRUXBUY
      BOZUVBUWBUKUVEUURUYIYRUURUYINUUFUULYRUURUYIYRUUROZUXBUYBUYJUYEUWRUXBUURUY
      DYRUYHYBZCUUQUGUXDWAUYJUYEUYAUYBUYKUYFUYGWAXRXNWGURUUSUUTYSYCUOYDXNYEXNUU
      MUVCAUUQGYTUUQMZUUJUVBUYLUUCUVAYSUYLUUAUUSUUBUUTKYTUUQCHYFYTUUQCJYFYGYHYI
      YJYKUUJAGYLYMYNXEYOYPXEXNYQUR $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)
  $}

  ${
    $d A x $.
    $( Lemma for ~ iint . $)
    iintlem2 $p |- ( y e. |^|_ x e. RR+ ( ( A - x ) (,) ( A + x ) )
                    -> y e. RR ) $=
      ( crp cv cmin co caddc cioo ciin cr wss wrex c1 wcel 1rp ioossre wceq
      opreq2 opreq12d sseq1d rcla4ev mp2an iinss ax-mp sseli ) ADCAEZFGZCUGHGZI
      GZJZKBEUJKLZADMZUKKLNDOCNFGZCNHGZIGZKLZUMPUNUOQULUQANDUGNRZUJUPKURUHUNUIU
      OIUGNCFSUGNCHSTUAUBUCADUJKUDUEUF $.
      $( [3-Jan-2008] $) $( [23-Dec-2007] $)
  $}


  ${
    $d A x y $.
    $( Indexed intersection of a set of open intervals centered on ` A ` .
       This theorem is a rough justification for taking finite intersections in
       the definition of a topology.  If we consider we are in the standard
       topology of ` RR ` this theorem means a non finite intersection of open
       sets can result in a closed set. $)
    iint $p |- ( A e. RR -> |^|_ x e. RR+ ( ( A - x ) (,) ( A + x ) ) = { A } )
        $=
      ( vy cr wcel crp cv cmin co caddc cioo ciin wceq crab csn wa iintlem2
      adantl iintlem1 jcai ex wi eleq1 wral cxr w3a clt wbr resubcl rpre
      sylan2 rexr syl readdcl adantr 3jca ltsubpostb ltaddpos2tb jca wb
      elioo3g mpbird r19.21aiva eliin syl5bir com12 impbid eqeq1 elrab syl6bbr
      eqrdv rabsn eqtrd ) BDEZAFBAGZHIZBVOJIZKIZLZVOBMZADNZBOVNCVSWAVNCGZVSEZWB
      DEZWBBMZPZWBWAEVNWCWFVNWCWFVNWCPWDWEWCWDVNACBQRACBSTUAWFVNWCWEVNWCUBWDWEW
      CBVSEZVNWBBVSUCVNWGBVREZAFUDVNWHAFVNVOFEZPZWHVPUEEZVQUEEZBUEEZUFZVPBUGUHZ
      BVQUGUHZPZPZWJWNWQWJWKWLWMWJVPDEZWKVNVODEZWSWIBVOUIVOUJZUKVPULUMWJVQDEZWL
      VNWTXBWIBVOUNXAUKZVQULUMVNWMWIBULUOUPWJWOWPBVOUQBVOURUSUSWJXBWHWRUTXCVPVQ
      BDVAUMVBVCABFVRDVDVBVERVFVGVTWEAWBDVOWBBVHVIVJVKADBVLVM $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)
  $}

  ${
    $d A x $.
    trdom.1 $e |- F = ( x e. RR |-> ( x + A ) ) $.
    $( Domain of a translation. $)
    trdom $p |- ( A e. RR -> dom F = RR ) $=
      ( cr wcel cv caddc co cvv wral cdm wceq wa readdcl elisset syl ancoms
      r19.21aiva cmpdom sylib ) BEFZAGZBHIZJFZAEKCLEMUBUEAEUCEFZUBUEUFUBNUDEFUE
      UCBOUDEPQRSAEUDCDTUA $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A x y $.
    trran.1 $e |- F = ( x e. RR |-> ( x + A ) ) $.
    $( Range of a translation. $)
    trran $p |- ( A e. RR -> ran F = RR ) $=
      ( vy cr wcel cv caddc co wceq wrex cab crn wi wa eleq1 readdcl syl5cbir
      expcom r19.23adv cmin resubcl cc npcan recn syl2an eqcomd opreq1 eqeq2d
      rcla4ev syl11anc impbid abbi1dv cmpran syl5eq ) BFGZEHZAHZBIJZKZAFLZEMFCN
      UQVBEFUQVBURFGZUQVAVCAFUSFGZUQVAVCOVAVCUTFGVDUQPURUTFQUSBRSTUAVCUQVBVCUQP
      ZURBUBJZFGURVFBIJZKZVBURBUCVEVGURURUDGBUDGVGURKVCUQURBUEURUFBUFUGUHVAVHAV
      FFUSVFKUTVGURUSVFBIUIUJUKULTUMUNAEFUTCDUOUP $.
      $( [17-Feb-2008] $)
  $}


  ${
    $d A u v x y $.  $d F x y $.
    trnij.1 $e |- F = ( x e. RR |-> ( x + A ) ) $.
    $( A translation is 1-1-onto. $)
    trnij $p |- ( A e. RR -> F : RR -1-1-onto-> RR ) $=
      ( vy vv vu cr wcel wf1 wfo wa wf1o wf cv cfv wceq wi wral wfn crn wss
      wfun cdm trdom caddc co cmpfun jctil df-fn sylibr trran eqimss anim2i
      syl11anc df-f readdcl expcom anim12d imp cmpt simpr fveq1i copab df-mpt
      wb eleq1 adantr eqeq1 opreq1 eqeq2d sylan9bbr anbi12d cbvopabv 3eqtri
      eqtr4i 3eqtr3g simprl ad2antrr simplrl eqid fvopab2b simplrr simprr jca
      syl 3eqtr3d cc w3a recn ad2antrl ad2antll 3jca addcan2 mpbid exp31 mpd
      ex r19.21aivv dff13 df-fo df-f1o ) BHIZHHCJZHHCKZLHHCMXCXDXEXCHHCNZAOZCPZ
      EOZCPZQZXGXIQZRZEHSAHSZLXDXCXFXNXCCHTZCUAZHUBZLZXFXCXOXPHQZXRXCCUCZCUDHQZ
      LXOXCYAXTABCDUEAHXGBUFUGZCDUHUICHUJUKZABCDULZXSXQXOXPHUMUNUOHHCUPUKXCXMAE
      HHXCXGHIZXIHIZLZXMXCYGLZYBHIZXIBUFUGZHIZLZXMXCYGYLXCYEYIYFYKYEXCYIXGBUQUR
      YFXCYKXIBUQURUSUTYHYLXKXLYHYLLZXKLZYBYJQZXLYNXGAHYBVAZPZXIEHYJVAZPZYBYJYN
      XHXJYQYSYMXKVBXGCYPDVCXICYRCYFFOZYJQZLZEFVDZYRCYPYEGOZYBQZLZAGVDUUCDAGHYB
      VEUUFUUBAGEFXLUUDYTQZLYEYFUUEUUAXLYEYFVFUUGXGXIHVGVHUUGUUEYTYBQXLUUAUUDYT
      YBVIXLYBYJYTXGXIBUFVJVKVLVMVNVOEFHYJVEVPVCVQYNYEYIYQYBQYHYEYLXKXCYEYFVRVS
      YHYIYKXKVTAHYBHYPYPWAWBUOYNYFYKLZYSYJQYMUUHXKYMYFYKXCYEYFYLWCYHYIYKWDWEVH
      EHYJHYRYRWAWBWFWGYNXGWHIZXIWHIZBWHIZWIZYOXLVFYHUULYLXKYHUUIUUJUUKYEUUIXCY
      FXGWJWKYFUUJXCYEXIWJWLXCUUKYGBWJVHWMVSXGXIBWNWFWOWPWQWRWSWEAEHHCWTUKXCXOX
      SLXEXCXOXSYCYDWEHHCXAUKWEHHCXBUK $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A u v x y $.  $d A w x y z $.
    $( Converse of a translation. $)
    cnvtr $p |- ( A e. RR -> `' ( x e. RR |-> ( x + A ) ) =
      ( x e. RR |-> ( x - A ) ) ) $=
      ( vu vy vv vz vw cr wcel cv caddc co cmpt ccnv cmin wceq cfv wral wa
      wf1o copab ax-17 wb eleq1 adantr eqeq1 opreq1 eqeq2d sylan9bbr anbi12d
      cbvopab df-mpt 3eqtr4i trnij resubcl ancoms jca cbvopabv 3eqtr4ri a1i
      fveq1d readdcl sylancom fvopab4g cc npcan recn syl2an sylan9eqr mpdan
      eqtrd f1ocnvfv sylc simpr eqid fvopab2b syl11anc eqtr4d r19.21aiva jctil
      wfn dff1o4 sylib simprd cvv elisset syl fopab2ga eqidd hbmpt1 hbcnv
      eqfnfv2f biimpa ex wi biimprcd adantl com12 impbid mpbird ) BHIZAHAJZBKLZ
      MZNZAHXBBOLZMZPZHHPZXBXEQZXBXGQZPZAHRZSZXAXMXIXAXLAHXAXBHIZSZXJXFXKXPHHXD
      TZXFHIZSXFXDQZXBPXJXFPXPXQXRXAXQXOCBXDXODJZXCPZSZADUACJZHIZEJZYCBKLZPZSZC
      EUAXDCHYFMYBYHADCEYBCUBYBEUBYHAUBYHDUBXBYCPZXTYEPZSXOYDYAYGYIXOYDUCYJXBYC
      HUDUEYJYAYEXCPYIYGXTYEXCUFYIXCYFYEXBYCBKUGUHUIUJUKADHXCULCEHYFULUMUNZUEXO
      XAXRXBBUOZUPZUQXPXSXFDHXTBKLZMZQZXBXPXFXDYOXDYOPXPXTHIZFJZYNPZSZDFUAXOGJZ
      XCPZSZAGUAYOXDYTUUCDFAGXTXBPZYRUUAPZSYQXOYSUUBUUDYQXOUCUUEXTXBHUDUEUUEYSU
      UAYNPUUDUUBYRUUAYNUFUUDYNXCUUAXTXBBKUGUHUIUJURDFHYNULZAGHXCULUSUTVAXPXRXF
      BKLZHIZSZYPXBPXOXAUUIXOXASZXRUUHYLXOXAXRUUHYLXFBVBVCUQUPUUIXPYPUUGXBDFXFY
      NUUGHHYOXTXFBKUGUUFVDXOXAUUGXBPZXBVEIBVEIUUKXOXAXBBVFXBVGBVGVHUPVIVJVKHHX
      FXBXDVLVMXPXOXRXKXFPXAXOVNYMAHXFHXGXGVOZVPVQVRVSHVOVTXAXEHWAZXGHWAZXHXNUC
      XAXDHWAZUUMXAXQUUOUUMSYKHHXDWBWCWDXAXFWEIZAHRUUNXAUUPAHXOXAUUPUUJXRUUPYLX
      FHWFWGUPVSAHXFXGUULWHWCUUMUUNSZXHXNUUQXHXNUUQXHSZXIXMUURHWIUUQXHXMAGHXEXG
      AGXDAGHXCWJWKAGHXFWJWLZWMUQWNXNUUQXHXMUUQXHWOXIUUQXHXMUUSWPWQWRWSVQWT $.
      $( [3-Aug-2009] $)
  $}

  $( The members of a 'less than or equal' relationship are extended reals. $)
  mlteqer $p |- ( B e. C -> ( A <_ B -> ( A e. RR* /\ B e. RR* ) ) ) $=
    ( cle wbr wcel cxr wa cop wi df-br cxp clt ccnv cdif df-le eleq2i wn eldif
    opelxpg biimpcd adantr sylbi com12 ) ABDEZBCFZAGFBGFHZUEABIZDFZUFUGJZABDKUI
    UHGGLZMNZOZFZUJDUMUHPQUNUHUKFZUHULFRZHUJUHUKULSUOUJUPUFUOUGABGGCTUAUBUCUCUC
    UD $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  $( Transitive law for ordering on extended reals ( compare ~ xrletr ). $)
  xrletr2 $p |- ( C e. D ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cle wbr wa wcel wi cxr mlteqer w3a xrletr exp3a 3expia com23 syl6 com3l
    pm2.43i com4l imp impcom com12 ) ABEFZBCEFZGCDHZACEFZUEUDUFUGIZUEUDUHIUFUEU
    EUDUGUFUEBJHZCJHZGUEUDUGIIZBCDKUIUJUKUDUIUJUEUGUDUIUJUEUGIZIZIUIUDUDUMUIUDA
    JHZUIGZUDUMIABJKUOUJUDULUNUIUJUDULIUNUIUJLUDUEUGABCMNOPQRSTUAQTSUBUC $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( Trichotomy law. $)
  letri31 $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A = B <-> ( A <_ B /\ B <_ A ) ) ) $=
    ( cxr wcel wa wceq clt wbr wn cle xrlttri3 ancom syl6bbr xrlenlt wb ancoms
    anbi12d bitr4d ) ACDZBCDZEZABFZBAGHIZABGHIZEZABJHZBAJHZEUAUBUDUCEUEABKUCUDL
    MUAUFUCUGUDABNTSUGUDOBANPQR $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  ${
    $d x y $.
    $( domain of ` <_ ` is ` RR* ` . $)
    domleqt $p |- dom <_ = RR* $=
      ( vx vy cle cdm cv wbr wex cab cxr df-dm wceq wcel wb abeq1 cvv wa wi
      visset mlteqer ax-mp simpld 19.23aiv cpnf pnfge pnfxr elisseti breq2
      imbi2d cla4ev 19.37aiv impbii mpgbir eqtri ) CDAEZBEZCFZBGZAHZIABCJURIKUQ
      UNILZMAUQAINUQUSUPUSBUPUSUOILZUOOLUPUSUTPQBRUNUOOSTUAUBUSUPBUSUNUCCFZQZUS
      UPQZBGUNUDVCVBBUCUCIUEUFUOUCKUPVAUSUOUCUNCUGUHUITUJUKULUM $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  $( The range of ` <_ ` is included in the extended reals. $)
  ranleqt $p |- ran <_ C_ RR* $=
    ( cle crn cxr cxp clt ccnv cdif df-le rneqi wss difss rnss ax-mp c0 wne
    wceq cpnf wcel pnfxr ne0i rnxp sseqtri eqsstri ) ABCCDZEFZGZBZCAUFHIUGUDBZC
    UFUDJUGUHJUDUEKUFUDLMCNOZUHCPQCRUISCQTMCCUAMUBUC $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( 'Less or equal to' is a relation. $)
  leqrl $p |- Rel <_ $=
    ( cle wrel cxr cxp clt ccnv cdif relxp reldif ax-mp df-le releqi mpbir ) AB
    CCDZEFZGZBZNBQCCHNOIJAPKLM $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( The field of the 'less or equal to' relationship on the extended real. $)
  fldleqt $p |- U. U. <_ = RR* $=
    ( vx cle cuni cdm crn cun cxr wrel wceq leqrl relfld ax-mp cv wcel wo
    domleqt eleq2i biimpi ranleqt sseli jaoi biimpri orcd impbii uneqri eqtri
    ) BCCZBDZBEZFZGBHUGUJIJBKLAUHUIGAMZUHNZUKUINZOUKGNZULUNUMULUNUHGUKPQZRUIGUK
    STUAUNULUMULUNUOUBUCUDUEUF $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  ${
    $d x y z $.
    $( The "less than or equal to" relationship on the extended reals is a
       poset. $)
    lteqtpos $p |- <_ e. Poset $=
      ( vx vy vz cle cvv wcel cps cxr cxp clt ccnv cdif df-le xrex xpex difexg
      ax-mp eqeltri wrel ccom wss cin cid cuni cres wceq w3a leqrl cv wbr wa
      wi wal visset xrletr2 ax-gen gen2 cotr mpbir wb asymref fldleqt eleq2i
      mlteqer letri31 biimprd exp3a com23 ancoms syl6 com4l pm2.43i impcom
      com12 eleq1 biimpd ex com3l syl6bi pm2.43a com13 impbid 19.21aiv sylbi
      mprgbir 3pm3.2i isps mpbiri ) DEFZDGFZDHHIZJKZLZEMWKEFWMEFHHNNOWKWLEPQRWI
      WJDSZDDTDUAZDDKUBUCDUDUDZUEUFZUGWNWOWQUHWOAUIZBUIZDUJZWSCUIZDUJUKWRXADUJU
      LZCUMZBUMAUMXCABXBCXAEFXBCUNWRWSXAEUOQUPUQABCDURUSWQWTWSWRDUJZUKZWRWSUFZU
      TZBUMZAWPABDVAWRWPFWRHFZXHWPHWRVBVCXIXGBXIXEXFXEXIXFXDWTXIXFULZXDWTXJULXI
      XDXDWTXFXIXDWSHFZXIUKXDWTXFULULZWSWRHVDXIXKXLXIXKUKZWTXDXFXMWTXDXFXMXFXEW
      RWSVEZVFVGVHVIVJVKVLVMVNXIXFXEULZXFXIXIXEXIXFXIXEULZXFXIXKXFXPULWRWSHVOXI
      XKXFXEXIXKXOXMXFXEXNVPVQVRVSVTWAVLWBWCWDWEWFEDWGWHQ $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}


