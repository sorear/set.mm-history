$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Pre-calculus and Cartesian geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Distance between the middle of a segment and one of its extremities is a
     positive real. $)
  dmse1 $p |- ( ( A e. RR /\ B e. RR /\ A =/= B ) ->
    ( ( abs ` ( A - B ) ) / 2 ) e. RR+ ) $=
    ( cr wcel wne w3a cmin co cabs cfv crp c2 wa cdiv cc cc0 absrpcl resubcl
    recnd 3adant3 wceq wn wb subeq0 recn syl2an biimpd necon3ad 3impia df-ne
    sylibr sylanc 2re 2pos elrpii jctir rpdivcl syl ) ACDZBCDZABEZFZABGHZIJZKDZ
    LKDZMVDLNHKDVBVEVFVCODZVCPEZVEVBVCQUSUTVGVAUSUTMZVCABRSTVBVCPUAZUBZVHUSUTVA
    VKVIVJABVIVJABUAZAODBODVJVLUCUSUTABUDAUEBUEUFUGUHUIVCPUJUKULLUMUNUOUPVDLUQU
    R $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( Distance between the middle of a segment and one of its extremities is a
     positive real. $)
  dmse2 $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
    ( ( abs ` ( A - B ) ) / 2 ) e. RR+ ) $=
    ( cr wcel wne cmin co cabs cfv c2 cdiv crp clt wbr dmse1 w3a ltne necomd
    syld3an3 ) ACDZBCDZABEABFGHIJKGLDABMNZABOTUAUBPBAABQRS $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( The midpoint of a segment AB of the real line is a real. $)
  msr3 $p |- ( ( A e. RR /\ B e. RR ) ->
   ( B - ( ( abs ` ( A - B ) ) / 2 ) ) e. RR ) $=
    ( cr wcel cmin co cabs cfv c2 cdiv wa resubcl pm3.27 cc0 wne redivcl cc
    recnd abscl syl 2re a1i 2ne0 syl3anc sylanc ) BCDZABEFZGHZIJFZCDZBUIEFCDACD
    ZUFKZBUILUKUFMUHCDZICDZINOZUJULUHIPULUGQDUMULUGABLRUGSTUNULUAUBUOULUCUBUDUE
    $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( The midpoint of a segment AB of the real line is a real.  (To FL:  This
     proof was shortened.  Also, it is too specialized, and set.mm size will
     be reduced if it is placed directly in the proof using it. -NM) $)
  msr4 $p |- ( ( A e. RR /\ B e. RR ) ->
    ( A + ( ( abs ` ( B - A ) ) / 2 ) ) e. RR ) $=
    ( cr wcel cmin co cabs cfv c2 cdiv caddc wa cc resubcl recnd ancoms abscl
    rehalfcl 3syl readdcl syldan ) ACDZBCDZBAEFZGHZIJFZCDZAUFKFCDUBUCLUDMDZUECD
    UGUCUBUHUCUBLUDBANOPUDQUERSAUFTUA $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( Translation and inequality on the real line. $)
  ltsubpostb $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A - B ) < A ) $=
    ( cr wcel cc0 clt wbr wa cmin co crp wi ltsubpos biimpd expcom imp32 elrp
    sylan2b ) ACDZBCDZEBFGZHABIJAFGZBKDSTUAUBTSUAUBLTSHUAUBBAMNOPBQR $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( Translation and inequality on the real line. $)
  ltaddpos2tb $p |- ( ( A e. RR /\ B e. RR+ ) -> A < ( A + B ) ) $=
    ( cr wcel cc0 clt wbr wa caddc co crp wi ltaddpos biimpd expcom imp32 elrp
    sylan2b ) ACDZBCDZEBFGZHAABIJFGZBKDSTUAUBTSUAUBLTSHUAUBBAMNOPBQR $.
    $( [3-Jan-2008] $) $( [27-Dec-2007] $)

  $( The midpoint of a segment AB of the real line is on the "left" of
     ` B ` . $)
  mslb1 $p |- ( ( A e. RR /\ B e. RR /\ A < B  ) ->
     ( A + ( ( abs ` ( B - A ) ) / 2 ) ) < B ) $=
    ( cr wcel clt wbr w3a c2 cmul co cmin caddc cdiv cabs cfv wceq cc adddi
    2cn a1i recn 3ad2ant1 crp wa 3simpa anim12i ancoms abssub 3syl opreq1d
    dmse2 eqeltrd rpre syl3anc cc0 wne divcan2 subcl abscl 2ne0 cle absid
    resubcl 3adant3 ltle 3impia wb pm3.22 subge0 mpbird sylanc eqtrd opreq2d
    pm3.2i divmul mp3an3 readdcl 2re remulcl mpan adantr recnd rehalfcl syldan
    3simp3 3simp1 2times pm3.27 jca 3ad2ant2 pncan syl breqtrrd 3simp2 3brtr4d
    eqbrtrd mpdan cneg negsub sub23 negid subid eqcomd subadd anidms adantl
    negcl 3eqtr2rd mulcl 3jca addass eqtr4d addcl 3eqtr3d breq1d biimpa ltsub1
    2pos ltdivmul eqbrtrrd ) ACDZBCDZABEFZGZHAIJZBAKJZLJZHMJZAYFNOZHMJZLJZBEYDY
    HYKPZHYKIJZYGPZYDYMYEHYJIJZLJZYGHQDZAQDZYJQDZYMYPPYDHAYJRYQYDSTZYAYBYRYCAUA
    ZUBZYDYJUCDYJCDZYSYDYJABKJNOZHMJUCYDYIUUDHMYDYAYBUDZBQDZYRUDZYIUUDPYAYBYCUE
    ZYBYAUUGYBUUFYAYRBUAZUUAUFUGZBAUHUIUJABUKULYJUMYJUAUIUNYDYOYFYELYDYOYIYFYIQ
    DZYQHUOUPZYOYIPYDYIHUQYDYFQDZYICDZUUKYDUUEUUGUUMUUHUUJBAURZUIYFUSZYIUAUIYTU
    ULYDUTTUNYFCDZUOYFVAFZYIYFPYDYFVBYAYBUUQYCYBYAUUQBAVCZUGZVDZYDUURABVAFZYAYB
    YCUVBABVEVFYDUUEYBYAUDZUURUVBVGUUHYAYBVHBAVIUIVJVKVLVMVLYAYBYLYNVGZYCYGQDZY
    KQDZUVDUUEUVEUVFYQUULUDUVDYQUULSUTVNYGYKHVOVPUUEYGYECDZUUQYGCDZUUEYEYFVQZYA
    UVGYBHCDZYAUVGVRHAVSVTZWAUUTVKWBUUEYKYAYBUUCYKCDUUEUUMUUNUUCYBYAUUMUVCYFUUS
    WBUGUUPYIWCUIAYJVQWDWBVKVDVJYDYHBEFZYGHBIJZEFZYDYEAKJZUVMBKJZEFZUVNYDYCUVQY
    AYBYCWEYDYCUDZUVOAALJZAKJZUVPEUVRYEUVSAKYDYEUVSPZYCYDYAYRUWAYAYBYCWFZUUAAWG
    UIWAUJUVRABBLJZBKJZUVTUVPEUVRABUWDEYDYCWHUVRUUFUUFUDZUWDBPYDUWEYCYBYAUWEYCY
    BUUFUUFUUIUUIWIWJWABBWKWLWMUVRYRYRUDZUVTAPYDUWFYCYAYBUWFYCYAYRYRUUAUUAWIUBW
    AAAWKWLUVRUVMUWCBKYDUVMUWCPZYCYDYBUUFUWGYAYBYCWNZUUIBWGUIWAUJWOWPWQYDUVQUDZ
    UVNYGBKJZUVPEFZYDUVQUWKYDUVOUWJUVPEYDYEAWRZLJZYGBWRZLJZUVOUWJYDUWMYEYFUWNLJ
    ZLJZUWOYDUWLUWPYELYDUWPYFBKJZBBKJZAKJZUWLUUMUUFUWPUWRPYDYFBWSYDUUEYRUUFUDZU
    UMUUHYAYRYBUUFUUAUUIUFZUUFYRUUMUUOUGZUIYBYAUUFYCUUIWJZVKUUFUUFYRUWTUWRPYDBB
    AWTUXDUXDUUBUNYDUWTUWLPZAUWLLJZUWSPZYDUXFUOUWSYDYAYRUXFUOPUWBUUAAXAUIYBYAUO
    UWSPYCYBUWSUOYBUUFUWSUOPUUIBXBWLXCWJVLUWSQDZYRUWLQDZUXEUXGVGYDUWSAUWLXDYDUU
    EUXAUXHUUHUXBUUFUXHYRUUFUXHBBURXEXFUIUUBYDYAYRUXIUWBUUAAXGUIUNVJXHVMYDYEQDZ
    UUMUWNQDZGZUWOUWQPYDUUEUXAUXLUUHUXBUXAUXJUUMUXKYRUXJUUFYQYRUXJSHAXIVTZWAZUX
    CUUFUXKYRBXGXFXJUIYEYFUWNXKWLXLUXJYRUWMUVOPYDYEAWSYDYAYRUXJUWBUUAUXMUIUUBVK
    YDUVEUUFUDZUWOUWJPYDUUEUXAUXOUUHUXBUXAUVEUUFUXJUUMUVEUXAYEYFXMUXNUXCVKYRUUF
    WHWIUIYGBWSWLXNXOXPUWIUVHUVMCDZYBGZUVNUWKVGYDUXQUVQYDUVHUXPYBUVGUUQUVHYDUVI
    YAYBUVGYCUVKUBUVAVKZYBYAUXPYCUVJYBUXPVRHBVSVTWJUWHXJWAYGUVMBXQWLVJWQUVHYBUV
    LUVNVGZYDUVHYBUVJUOHEFZUDUXSUVJUXTVRXRVNYGBHXSVPUXRUWHVKVJXT $.
    $( [3-Jan-2008] $) $( [2-Jan-2008] $)

  $( Two ways to state the midpoint of a segment. $)
  2wsms $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
    ( ( A + B ) / 2 ) = ( B - ( ( abs ` ( A - B ) ) / 2 ) ) ) $=
    ( cr wcel clt wbr w3a c2 caddc co cdiv cmul cmin cabs cfv wceq cc addass
    eqcomd cc0 wne divcan2 wa 3simpa recn anim12i subcl 3syl abscl 2cn a1i
    2ne0 syl3anc resubcl eqeltrd 3ad2ant1 3ad2ant2 addcom addcl sylanc 3simp2
    2times opreq1d jca pncan cle abssuble0 ltle 3impia syld3an3 eqtr2d wb
    subadd mpbid eqtr3d 3eqtrd mulcl mpan mpbird subdi crp dmse2 rpre 3eqtr4rd
    mulcant2i halfaddsubcl pm3.26d msr3 ) ACDZBCDZABEFZGZHABIJZHKJZLJZHBABMJZNO
    ZHKJZMJZLJZPZWNWSPZWLHBLJZHWRLJZMJZWMWTWOWLXEWMPZXDWMIJZXCPZWLXGXDAIJZBIJZB
    XIIJZXCXDQDZAQDZBQDZXGXJPWLXLXMXNGXJXGXDABRSWLXDWQQWQQDZHQDZHTUAZXDWQPWLWQH
    UBWLWPQDZWQCDZXOWLWIWJUCZXMXNUCZXRWIWJWKUDZWIXMWJXNAUEZBUEZUFZABUGUHWPUIZWQ
    UEZUHXPWLUJUKZXQWLULUKZUMZWLXRXSXOWLXTWPCDXRYBABUNWPUEUHYFYGUHUOZWIWJXMWKYC
    UPZWJWIXNWKYDUQZUMXIQDZXNXJXKPWLXIBURXLXMYNWLXDAUSYKYLUTZYMUTWLXCBMJZXIPZXK
    XCPZWLYPBBIJZBMJZBXIWLXCYSBMWLWJXNXCYSPWIWJWKVAZYDBVBUHVCWLWJXNXNUCYTBPUUAW
    JXNXNYDYDVDBBVEUHWLAXDIJZBXIWLBAMJZXDPZUUBBPZWLXDWQUUCYJWIWJABVFFZWQUUCPWKA
    BVGWIWJWKUUFABVHVIVJVKXNXMXLUUDUUEVLWLBAXDVMYMYLYKUMVNXMXLUUBXIPWLAXDURYLYK
    UTVOVPXCQDZXNYNYQYRVLWLXCBXIVMWLWJXNUUGUUAYDXPXNUUGUJHBVQVRUHZYMYOUMVNVPUUG
    XLWMQDZXFXHVLWLXCXDWMVMUUHYKWLXTYAUUIYBYEABUSUHZUMVSXPXNWRQDZWTXEPWLHBWRVTY
    HYMWLWRWADWRCDUUKABWBWRWCWRUEUHUMUUIXPXQWOWMPWLWMHUBUUJYHYIUMWDWNQDZWSQDZXP
    XAXBVLWLWNWSHULWEWLXTYAUULYBYEYAUULWPHKJQDABWFWGUHWLXTWSCDUUMYBABWHWSUEUHYH
    UMVN $.
    $( [3-Jan-2008] $) $( [3-Jan-2008] $)

  $( The midpoint of a segment AB of the real line is on the "right" of
     ` A ` . $)
  msra3 $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
    A < ( B - ( ( abs ` ( A - B ) ) / 2 ) ) ) $=
    ( cr wcel clt wbr w3a caddc co c2 cdiv cmin cabs cfv cmul wa cc wceq
    3simpa recn adantr 2cn jctir mulcom 3syl 3simp1 2times eqtrd 3simp3 wb jca
    pncan breq1d mpbird ltsubadd2 readdcl sylanc 3simp2 syl3anc mpbid eqbrtrd
    cc0 2re 2pos pm3.2i ltmuldiv mp3an3 3adant3 2wsms breqtrd ) ACDZBCDZABEFZGZ
    AABHIZJKIZBABLIMNJKILIEVNAJOIZVOEFZAVPEFZVNVQAAHIZVOEVNVQJAOIZVTVNVKVLPZAQD
    ZJQDZPVQWARVKVLVMSZWBWCWDVKWCVLATZUAUBUCAJUDUEVNVKWCWAVTRVKVLVMUFZWFAUGUEUH
    VNVTALIZBEFZVTVOEFZVNWIVMVKVLVMUIVNWBWCWCPZWIVMUJWEVKWKVLVKWCWCWFWFUKUAWKWH
    ABEAAULUMUEUNVTCDZVKVLWIWJUJVNVTABUOVKVKWLVNAAUPWGWGUQWGVKVLVMURUSUTVAVKVOC
    DZVRVSUJZVNVKWMJCDZVBJEFZPWNWOWPVCVDVEAVOJVFVGWGVKVLWMVMABUPVHUQUTABVIVJ $.
    $( [3-Jan-2008] $) $( [3-Jan-2008] $)

  ${ $d A x  $.  $d x y $.
    $( Lemma for ~ iint . $)
    iintlem1 $p |- ( ( A e. RR
                          /\ y e. |^|_ x e. RR+ ( ( A - x ) (,) ( A + x ) ) )
                       -> ( y e. RR -> y = A ) ) $=
      ( cr wcel cv crp cmin co caddc cioo ciin wceq wi wa wral eliin wn wrex
      cabs cfv c2 cdiv cc cc0 wne absrpcl resubcl ancoms recnd adantr wb recn
      anim12i subeq0 syl biimpd con3d imp df-ne sylibr sylanc 2re 2pos elrpii
      jctir rpdivcl cxr clt wbr w3a cle wo simplr ad2antrl simpll 0re
      ltsubadd2 3simp2 addid1 3syl breq2d bitrd 3exp com13 ax-mp ad2antrr
      impcom 3jca msra3 orc leloe pm3.22 adantl msr3 mpbird xrlenlt rexr mpbid
      intn3an2d jctil subge0 ltlen biimprcd com12 exp3a syl5ibr sylbid sylbird
      mslb1 msr4 readdcl ex rpre syl5 ad2antlr jca adantlr intn3an3d pm2.61ian
      anim2i elioo1 mtbird jcai opreq2 opreq12d eleq2d notbid rcla4ev syl6
      rexnal syl6ib con4d syl6bi pm2.43i com23 ) CDEZBFZAGCAFZHIZCYSJIZKIZLZEZY
      RDEZYRCMZNYQUUEUUDUUFYQUUEUUDUUFNUUDYQUUEOZUUFUUDUUGUUFNZUUDUUDYRUUBEZAGP
      ZUUHAYRGUUBUUCQUUGUUJUUFUUGUUFUUJUUGUUFRZUUIRZAGSZUUJRUUGUUKYRCHIZTUAZUBU
      CIZGEZYRCUUPHIZCUUPJIZKIZEZRZOZUUMUUGUUKUVCUUGUUKOZUUQUVBUVDUUOGEZUBGEZOU
      UQUVDUVEUVFUUNUDEZUUNUEUFZUVEUVDUUNUGUUGUVGUUKUUGUUNUUEYQUUNDEZYRCUHZUIUJ
      UKUVDUUNUEMZRZUVHUUGUUKUVLUUGUVKUUFUUGUVKUUFUUGYRUDEZCUDEZOZUVKUUFULUUEYQ
      UVOUUEUVMYQUVNYRUMCUMZUNUIYRCUOUPUQURUSUUNUEUTVAVBUBVCVDVEVFUUOUBVGUPUVDU
      UQUVBUVDUUQOZUVAYRVHEZUURYRVIVJZYRUUSVIVJZVKZUUNUEVIVJZUVQUWARUWBUVQOZUVS
      UVRUVTUWCYRUURVLVJZUVSRZUWCUWDYRUURVIVJZYRUURMZVMZUWCUUEYQYRCVIVJZVKUWFUW
      HUWCUUEYQUWIUVDUUEUWBUUQYQUUEUUKVNZVOZUVDYQUWBUUQYQUUEUUKVPZVOUVQUWBUWIUU
      GUWBUWINZUUKUUQYQUUEUWMUEDEZYQUUEUWMNNVQUUEYQUWNUWMUUEYQUWNUWMUUEYQUWNVKZ
      UWBUWIUWOUWBYRCUEJIZVIVJUWIYRCUEVRUWOUWPCYRVIUWOYQUVNUWPCMUUEYQUWNVSUVPCV
      TWAWBWCUQWDWEWFUSWGWHWIYRCWJUWFUWGWKWAUUEUURDEZUWDUWHULUWCYRUURWLUWKUWCUU
      EYQOZUWQUVQUWRUWBUUGUWRUUKUUQYQUUEWMWGZWNYRCWOZUPVBWPUVRUURVHEZUWDUWEULUW
      CYRUURWQUVQUVRUWBUUGUVRUUKUUQUUEUVRYQYRWRZWNWGWNUVQUXAUWBUVQUWRUWQUXAUWSU
      WTUURWRZWAWNVBWSWTUWBRZUVQOZUVTUVRUVSUXEUUSYRVLVJZUVTRZUXEUXFUUSYRVIVJZUU
      SYRMZVMZUXEYQUUECYRVIVJZVKUXHUXJUXEYQUUEUXKUVDYQUXDUUQUWLVOUVDUUEUXDUUQUW
      JVOZUVQUXDUXKUVDUXDUXKNUUQUVDUXDUEUUNVLVJZUXKUVDUEVHEZUUNVHEZOUXMUXDULUVD
      UXOUXNUUGUXOUUKUUEYQUXOUWRUVIUXOUVJUUNWRUPUIUKUWNUXNVQUEWRWFXAUEUUNWQUPUV
      DUXMCYRVLVJZUXKUUGUXMUXPULZUUKUUEYQUXQYRCXBUIUKUUGUUKUXPUXKNZUUGYRCUFZUXR
      UUKUUGUXSUXPUXKUXSUXPOUUGUXKUXPUXSUUGUXKNUUGUXKUXPUXSOCYRXCXDUIXEXFYRCUTX
      GUSXHXIUKWHWICYRXJUXHUXIWKWAUUSDEZUUEUXFUXJULUXEUUSYRWLUXEUUGUXTUVQUUGUXD
      UUGUUKUUQVPWNCYRXKUPUXLVBWPUXEUUSVHEZUVROZUXFUXGULUVQUYBUXDUUGUUQUYBUUKUU
      GUUQOUYAUVRUUGUUQUYAYQUUQUYANUUEYQUUPDEZUYAUUQYQUYCUYAYQUYCOZUXTUYACUUPXL
      ZUUSWRZUPXMUUPXNZXOUKUSUUEUVRYQUUQUXBXPXQXRWNUUSYRWQUPWSXSXTUVQUXAUYAOZUV
      AUWAULUVDUUQUYHYQUUQUYHNUUEUUKYQUUQUYHYQUUQOZUXAUYAUYIUYDUWQUXAUUQUYCYQUY
      GYAZCUUPUHUXCWAUYIUYDUXTUYAUYJUYEUYFWAXQXMWGUSUURUUSYRYBUPYCXMYDXMUULUVBA
      UUPGYSUUPMZUUIUVAUYKUUBUUTYRUYKYTUURUUAUUSKYSUUPCHYEYSUUPCJYEYFYGYHYIYJUU
      IAGYKYLYMXEYNYOXEXMYPUS $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)
  $}

  ${ $d A x $.
    $( Lemma for ~ iint . $)
    iintlem2 $p |- (  y e. |^|_ x e. RR+ ( ( A - x ) (,) ( A + x ) )
                    -> y e. RR ) $=
      ( crp cv cmin co caddc cioo ciin cr wss wrex c1 wcel 1rp ioossre wceq
      opreq2 opreq12d sseq1d rcla4ev mp2an iinss ax-mp sseli ) ADCAEZFGZCUGHGZI
      GZJZKBEUJKLZADMZUKKLNDOCNFGZCNHGZIGZKLZUMPUNUOQULUQANDUGNRZUJUPKURUHUNUIU
      OIUGNCFSUGNCHSTUAUBUCADUJKUDUEUF $.
      $( [3-Jan-2008] $) $( [23-Dec-2007] $)
  $}


  ${ $d A x y $.
    $( Indexed intersection of a set of open intervals centered on ` A ` .
       This theorem is a rough justification for taking finite intersections
       in the definition of a topology. If we consider we are in the
       standard topology of ` RR ` this theorem means a non finite
       intersection of open sets can result in a closed set. $)
    iint $p |- ( A e. RR -> |^|_ x e. RR+ ( ( A - x ) (,) ( A + x ) ) = { A } )
         $=
      ( vy cr wcel crp cv cmin co caddc cioo ciin wceq crab csn wa iintlem2
      adantl iintlem1 jcai ex wi eleq1 wral cxr w3a clt wbr resubcl rpre
      sylan2 rexr syl readdcl adantr 3jca ltsubpostb ltaddpos2tb jca wb
      elioo3g mpbird r19.21aiva eliin syl5bir com12 impbid eqeq1 elrab syl6bbr
      eqrdv rabsn eqtrd ) BDEZAFBAGZHIZBVOJIZKIZLZVOBMZADNZBOVNCVSWAVNCGZVSEZWB
      DEZWBBMZPZWBWAEVNWCWFVNWCWFVNWCPWDWEWCWDVNACBQRACBSTUAWFVNWCWEVNWCUBWDWEW
      CBVSEZVNWBBVSUCVNWGBVREZAFUDVNWHAFVNVOFEZPZWHVPUEEZVQUEEZBUEEZUFZVPBUGUHZ
      BVQUGUHZPZPZWJWNWQWJWKWLWMWJVPDEZWKVNVODEZWSWIBVOUIVOUJZUKVPULUMWJVQDEZWL
      VNWTXBWIBVOUNXAUKZVQULUMVNWMWIBULUOUPWJWOWPBVOUQBVOURUSUSWJXBWHWRUTXCVPVQ
      BDVAUMVBVCABFVRDVDVBVERVFVGVTWEAWBDVOWBBVHVIVJVKADBVLVM $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)
  $}

  ${ $d A x $.
    trdom.1 $e |- F = ( x e. RR |-> ( x + A ) ) $.
    $( Domain of a translation. $)
    trdom $p |- ( A e. RR -> dom F = RR ) $=
      ( cr wcel cv caddc co cvv wral cdm wceq wa readdcl elisset syl ancoms
      r19.21aiva cmpdom sylib ) BEFZAGZBHIZJFZAEKCLEMUBUEAEUCEFZUBUEUFUBNUDEFUE
      UCBOUDEPQRSAEUDCDTUA $.
      $( [17-Feb-2008] $) $( [17-Feb-2008] $)
  $}

  ${ $d A x y $.
    trran.1 $e |- F = ( x e. RR |-> ( x + A ) ) $.
    $( Range of a translation. $)
    trran $p |- ( A e. RR -> ran F = RR ) $=
      ( vy cr wcel cv caddc co wceq wrex cab crn wi wa eleq1 readdcl syl5cbir
      expcom r19.23adv cmin opreq1 eqeq2d rcla4ev resubcl cc npcan recn syl2an
      eqcomd sylanc impbid abbi1dv cmpran syl5eq ) BFGZEHZAHZBIJZKZAFLZEMFCNUQV
      BEFUQVBURFGZUQVAVCAFUSFGZUQVAVCOVAVCUTFGVDUQPURUTFQUSBRSTUAVCUQVBURBUBJZF
      GURVEBIJZKZVBVCUQPZVAVGAVEFUSVEKUTVFURUSVEBIUCUDUEURBUFVHVFURURUGGBUGGVFU
      RKVCUQURBUHURUIBUIUJUKULTUMUNAEFUTCDUOUP $.
      $( [17-Feb-2008] $) $( [17-Feb-2008] $)
  $}



  ${ $d A u v x y $.  $d F x y $.
    trnij.1 $e |- F = ( x e. RR |-> ( x + A ) ) $.
    $( A translation is 1-1-onto. $)
    trnij $p |- ( A e. RR -> F : RR -1-1-onto-> RR ) $=
      ( vy vv vu cr wcel wf1 wfo wa wf1o wf cv cfv wceq weq wi wral wfn crn
      wss eqimss anim2i wfun cdm trdom caddc co cmpfun jctil df-fn sylibr
      trran sylanc df-f readdcl expcom anim12d imp cmpt pm3.27 fveq1i copab
      df-mpt wb eleq1 adantr eqeq1 opreq1 eqeq2d sylan9bbr anbi12d cbvopabv
      3eqtri eqtr4i 3eqtr3g eqid fvopab2b simprl ad2antrr simplrl simplrr
      simprr jca syl 3eqtr3d cc w3a recn ad2antrl ad2antll 3jca addcan2 mpbid
      exp31 mpd ex r19.21aivv dff13 df-fo df-f1o ) BHIZHHCJZHHCKZLHHCMXDXEXFXDH
      HCNZAOZCPZEOZCPZQZAERZSZEHTAHTZLXEXDXGXOXDCHUAZCUBZHUCZLZXGXPXQHQZXSXDXTX
      RXPXQHUDUEXDCUFZCUGHQZLXPXDYBYAABCDUHAHXHBUIUJZCDUKULCHUMUNZABCDUOZUPHHCU
      QUNXDXNAEHHXDXHHIZXJHIZLZXNXDYHLZYCHIZXJBUIUJZHIZLZXNXDYHYMXDYFYJYGYLYFXD
      YJXHBURUSYGXDYLXJBURUSUTVAYIYMXLXMYIYMLZXLLZYCYKQZXMYOXHAHYCVBZPZXJEHYKVB
      ZPZYCYKYOXIXKYRYTYNXLVCXHCYQDVDXJCYSCYGFOZYKQZLZEFVEZYSCYQYFGOZYCQZLZAGVE
      UUDDAGHYCVFUUGUUCAGEFXMGFRZLYFYGUUFUUBXMYFYGVGUUHXHXJHVHVIUUHUUFUUAYCQXMU
      UBUUEUUAYCVJXMYCYKUUAXHXJBUIVKVLVMVNVOVPEFHYKVFVQVDVRYFYJYRYCQYOAHYCHYQYQ
      VSVTYIYFYMXLXDYFYGWAWBYIYJYLXLWCUPYOYGYLLZYTYKQYNUUIXLYNYGYLXDYFYGYMWDYIY
      JYLWEWFVIEHYKHYSYSVSVTWGWHYOXHWIIZXJWIIZBWIIZWJZYPXMVGYIUUMYMXLYIUUJUUKUU
      LYFUUJXDYGXHWKWLYGUUKXDYFXJWKWMXDUULYHBWKVIWNWBXHXJBWOWGWPWQWRWSWTWFAEHHC
      XAUNXDXPXTLXFXDXPXTYDYEWFHHCXBUNWFHHCXCUN $.
      $( [17-Feb-2008] $) $( [17-Feb-2008] $)
  $}

  ${ $d A u v x y $. $d A w x y z $.
    $( Converse of a translation. $)
    cnvtr $p |- ( A e. RR -> `' ( x e. RR |-> ( x + A ) ) =
      ( x e. RR |-> ( x - A ) ) ) $=
      ( vu vy vv vz vw cr wcel cv caddc co cmpt ccnv cmin wceq cfv wral wa
      wf1o f1ocnvfv copab ax-17 weq wb eleq1 adantr eqeq1 opreq1 eqeq2d
      sylan9bbr anbi12d cbvopab df-mpt 3eqtr4i trnij resubcl ancoms jca
      cbvopabv 3eqtr4ri a1i fveq1d readdcl sylancom fvopab4g cc npcan recn
      syl2an sylan9eqr mpdan eqtrd sylc eqid fvopab2b pm3.27 sylanc eqtr4d
      r19.21aiva jctil wfn eqidd hbcmpt hbcnv eqfnfvf2 biimpa ex wi biimprcd
      adantl com12 impbid dff1o4 sylib pm3.27d cvv elisset syl fopab2ga mpbird
      ) BHIZAHAJZBKLZMZNZAHXCBOLZMZPZHHPZXCXFQZXCXHQZPZAHRZSZXBXNXJXBXMAHXBXCHI
      ZSZXKXGXLHHXETZXGHIZSXGXEQZXCPXKXGPXQHHXGXCXEUAXQXRXSXBXRXPCBXEXPDJZXDPZS
      ZADUBCJZHIZEJZYDBKLZPZSZCEUBXECHYGMYCYIADCEYCCUCYCEUCYIAUCYIDUCACUDZDEUDZ
      SXPYEYBYHYJXPYEUEYKXCYDHUFUGYKYBYFXDPYJYHYAYFXDUHYJXDYGYFXCYDBKUIUJUKULUM
      ADHXDUNCEHYGUNUOUPZUGXPXBXSXCBUQZURZUSXQXTXGDHYABKLZMZQZXCXQXGXEYPXEYPPXQ
      YAHIZFJZYOPZSZDFUBXPGJZXDPZSZAGUBYPXEUUAUUDDFAGDAUDZFGUDZSYRXPYTUUCUUEYRX
      PUEUUFYAXCHUFUGUUFYTUUBYOPUUEUUCYSUUBYOUHUUEYOXDUUBYAXCBKUIUJUKULUTDFHYOU
      NZAGHXDUNVAVBVCXQXSXGBKLZHIZSZYQXCPXPXBUUJXPXBSZXSUUIYMXPXBXSUUIYMXGBVDVE
      USURUUJXQYQUUHXCDFXGYOUUHHHYPYAXGBKUIUUGVFXPXBUUHXCPZXCVGIBVGIUULXPXBXCBV
      HXCVIBVIVJURVKVLVMVNXPXSXLXGPXQAHXGHXHXHVOZVPXBXPVQYNVRVSVTHVOWAXFHWBZXHH
      WBZXIXOUEXBUUNUUOSZXIXOUUPXIXOUUPXISZXJXNUUQHWCUUPXIXNAGHXFXHAGXEAGHXDWDW
      EAGHXGWDWFZWGUSWHXOUUPXIXNUUPXIWIXJUUPXIXNUURWJWKWLWMXBXEHWBZUUNXBXRUUSUU
      NSYLHHXEWNWOWPXBXGWQIZAHRUUOXBUUTAHXPXBUUTUUKXSUUTYMXGHWRWSURVTAHXGXHUUMW
      TWOVRXA $.
      $( [3-Aug-2009] $) $( [3-Aug-2009] $)
  $}


