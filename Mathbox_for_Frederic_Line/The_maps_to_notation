$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              The "maps to" notation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    fnoprab2ga.1 $e |- F = ( x e. A , y e. B |-> C ) $.
    ${
      $d A x y z $.  $d B x y z $.  $d C z $.
      $( Functionality and domain of a class given by the "maps to" notation.
         Equivalent to ~ fnoprab2g . $)
      fnoprab2ga $p |- ( A. x e. A A. y e. B C e. _V <-> F Fn ( A X. B ) ) $=
        ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri fnoprab2g ) ABHCDEFFABC
        DEIAJCKBJDKLHJEMLABHNGABHCDEOPQ $.
        $( [18-May-2010] $) $( [17-May-2010] $)
    $}

    ${
      fnoprab2g.2 $e |- C e. _V $.
      ${
        $d A x y z $.  $d B x y z $.  $d C z $.
        $( Functionality and domain of a class given by the "maps to"
           notation.  Equivalent to ~ fnoprab2 . $)
        fnoprab2a $p |- F Fn ( A X. B ) $=
          ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri fnoprab2 ) ABICDEFHFA
          BCDEJAKCLBKDLMIKENMABIOGABICDEPQR $.
          $( [18-May-2010] $) $( [17-May-2010] $)

        $( Domain of a class given by the "maps to" notation.  Equivalent to
           ~ dmoprab2 . $)
        dmoprab2a $p |- dom F = ( A X. B ) $=
          ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri dmoprab2 ) ABICDEFHFA
          BCDEJAKCLBKDLMIKENMABIOGABICDEPQR $.
          $( [18-May-2010] $) $( [17-May-2010] $)
      $}

      ${
        $d A x y z $.  $d B x y z $.  $d C z $.  $d D x y z $.
        $( Functionality, domain and range of a class given by the "maps to"
           notation.  Equivalent to ~ foprab2 . $)
        foprab2a $p |- ( A. x e. A A. y e. B C e. D
          <-> F : ( A X. B ) --> D ) $=
          ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri foprab2 ) ABJCDEFGGAB
          CDEKALCMBLDMNJLEONABJPHABJCDEQRS $.
          $( [18-May-2010] $) $( [17-May-2010] $)
      $}

    $}

    ${
      $d A x y z $.  $d B x y z $.  $d C z $.  $d D x $.  $d X x y z $.
      $d Y x y z $.  $d Z y z $.
      oprabval2ga.1 $e |- ( x = X -> C = D ) $.
      oprabval2ga.2 $e |- ( y = Y -> D = Z ) $.
      $( Todo: change the variables. $)
      $( The value of an operation class abstraction.  Special case.
         Equivalent to ~ oprabval2g . $)
      oprabval2ga $p |- ( ( X e. A /\ Y e. B /\ Z e. N )
          -> ( X F Y ) = Z ) $=
        ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri oprabval2g ) ABOIJCDEKG
        FHMNGABCDEPAQCRBQDRSOQETSABOUALABOCDEUBUCUD $.
        $( [18-May-2010] $) $( [17-May-2010] $)

    $}

    ${
      $d A x y z $.  $d B x y z $.  $d C z $.
      $( Existence of an operation class abstraction (special case). $)
      oprabex2ga $p |- ( ( A e. R /\ B e. S ) -> F e. _V ) $=
        ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri oprabex2g ) ABJCDEFGHHA
        BCDEKALCMBLDMNJLEONABJPIABJCDEQRS $.
        $( [18-May-2010] $) $( [17-May-2010] $)
    $}

  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    fvopab2b.1 $e |- F = ( x e. A |-> B ) $.
    $( Value of a function given by the "maps to" notation.  Equivalent to
       ~ fvopab2 . $)
    fvopab2b $p |- ( ( x e. A /\ B e. C ) -> ( F ` x ) = B ) $=
      ( vy cv wcel wa wceq copab cfv fvopab2 cmpt df-mpt eqtri fveq1i syl5eq )
      AHZBIZCDIJTUAGHCKJAGLZMCTEMAGBCDNTEUBEABCOUBFAGBCPQRS $.
      $( [21-Jun-2010] $)
  $}

  ${
    $d A w x y z $.  $d B w y z $.  $d C w x z $.
    cmpbvb.1 $e |- F = ( x e. A |-> B ) $.
    cmpbvb.2 $e |- ( x = y -> B = C ) $.
    $( Rule to change a bound variable in the "maps to" notation. $)
    cmpbvb $p |- F = ( y e. A |-> C ) $=
      ( vz vw cmpt cv wcel wceq wa copab df-mpt simpl eleq1d simpr adantr
      eqeq12d anbi12d cbvopabv eqtr4i 3eqtri ) FACDKALZCMZILZDNZOZAIPZBCEKZGAIC
      DQULBLZCMZJLZENZOZBJPUMUKURAIBJUGUNNZUIUPNZOZUHUOUJUQVAUGUNCUSUTRSVAUIUPD
      EUSUTTUSDENUTHUAUBUCUDBJCEQUEUF $.
      $( [21-Jun-2010] $)
  $}

  ${
    $d A x y $.  $d C y $.
    fopab2ga.1 $e |- F = ( x e. A |-> C ) $.
    $( Functionality and domain of a class given by the "maps to" notation. $)
    fopab2ga $p |- ( A. x e. A C e. _V <-> F Fn A ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtri fnopab2g ) AFBCDDABCGAHBIFHC
      JKAFLEAFBCMNO $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C y $.
    fopab2a.1 $e |- F = ( x e. A |-> C ) $.
    $( Functionality of a class given by the "maps to" notation. $)
    fopab2a $p |- ( A. x e. A C e. B <-> F : A --> B ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtri fopab2 ) AGBCDEEABDHAIBJGIDK
      LAGMFAGBDNOP $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    cmp.1 $e |- F = ( x e. A |-> B ) $.
    $( Functionality of a class given by a "maps to" notation. $)
    cmpfun $p |- Fun F $=
      ( vy cmpt wfun cv wcel wceq wa copab wss simpr ssopab2i funopabeq funss
      mp2 wb df-mpt funeq ax-mp mpbir bicomd mpbi ) ABCGZHZDHZUHAIBJZFICKZLZAFM
      ZHZUMUKAFMZNUOHUNULUKAFUJUKOPAFCQUMUORSUGUMKUHUNTAFBCUAUGUMUBUCUDDUGKZUHU
      ITEUPUIUHDUGUBUEUCUF $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A x y $.  $d B y $.
    cmpdom.1 $e |- F = ( x e. A |-> B ) $.
    $( Domain of a class given by the "maps to" notation. $)
    cmpdom $p |- ( A. x e. A B e. _V <-> dom F = A ) $=
      ( cvv wcel wral cdm wceq wfun wfn wa df-fn fopab2ga ancom 3bitr4i cmpfun
      mpbiran2 ) CFGABHZDIBJZDKZDBLUBUAMTUAUBMDBNABCDEOUAUBPQABCDERS $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A x $.
    cmpdom2.1 $e |- F = ( x e. A |-> ( B G C ) ) $.
    $( Domain of a class given by the "maps to" notation. $)
    cmpdom2 $p |- dom F = A $=
      ( co cvv wcel wral cdm wceq cv oprex a1i rgen cmpdom mpbi ) CDFHZIJZABKEL
      BMUAABUAANBJCDFOPQABTEGRS $.
      $( [21-Jun-2010] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    cmpran.1 $e |- F = ( x e. A |-> B ) $.
    $( Range of a class given by the "maps to" notation. $)
    cmpran $p |- ran F = { y | E. x e. A y = B } $=
      ( crn cmpt cv wcel wceq wa copab wrex cab rneqi df-mpt rnopab2 3eqtri )
      EGACDHZGAICJBIDKZLABMZGUAACNBOETFPTUBABCDQPABCDRS $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A y z $.  $d B z $.  $d C z $.  $d x y z $.
    rngop.1 $e |- F = ( x e. A , y e. B |-> C ) $.
    $( The range of an operation given by the "maps to" notation. $)
    rngop $p |- ran F = { z | E. x e. A E. y e. B z = C } $=
      ( crn cv wcel wa wceq copab2 wex cab wrex cmpt2 df-mpt2 eqtri rneqi
      rnoprab r2ex bicomi abbii 3eqtri ) GIAJDKBJEKLCJFMZLZABCNZIUHBOAOZCPUGBEQ
      ADQZCPGUIGABDEFRUIHABCDEFSTUAUHABCUBUJUKCUKUJUGABDEUCUDUEUF $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C y z $.  $d F x z $.
    $( Functionality of an ordered-pair class abstraction given by the "maps
       to" notation. $)
    fopab2g $p |- ( F = { <. x , y >. | ( x e. A /\ y = C ) } ->
       ( A. x e. A C e. B <-> F : A --> B ) ) $=
      ( vz cv wcel wceq wa copab wral wf wfn crn wss weu cvv elisset eueq
      sylib ralimi adantl eqid fnopabg wb fneq1 adantr mpbird rneq sseq1d wi
      wal wex hbra1 ax-17 ra4 eleq1a imim2i imp3a syl 19.23ad rnopab abeq2i
      syl5ib 19.21aiv hbopab2 hbrn dfss2f sylibr syl5bir imp jca df-f cdm dmeq
      eqtr dmopab3 isset bicomi ralbii biimpi sylbir ex eqcoms fdm syl5
      hbopab1 hbeq hban cfv fvopab2 ffvelrn fveq1 eqcomd eqeq1d biimpac eleq1d
      biimpd com13 com3r com3l mpd ralimdaa impbida ) FAHZCIZBHZEJZKZABLZJZEDIZ
      ACMZCDFNZXMXOKZFCOZFPZDQZKXPXQXRXTXQXRXLCOZXQXJBRZACMZYAXOYCXMXNYBACXNESI
      ZYBEDTBEUAUBUCUDXJABCXLXLUEUFUBXMXRYAUGXOCFXLUHUIUJXMXOXTXMXTXLPZDQZXOXMX
      SYEDFXLUKULXOXIYEIZXIDIZUMZBUNYFXOYIBXOXKAUOZYHYGXOXKYHAXNACUPYHAUQXOXHXN
      UMZXKYHUMXNACURYKXHXJYHXNXJYHUMXHEDXIUSUTVAVBVCYJBYEXKABVDVEVFVGBGYEDBGXL
      XKABGVHVIGHZDIBUQVJVKVLVMVNCDFVOVKXMXPKZYDACMZXOXMXPYNXMFVPZCJZYNXPXMYOXL
      VPZJYPYNUMZFXLVQYRYQYOYQYOJZYPYNYSYPKYQCJZYNYQYOCVRYTXJBUOZACMZYNXJABCVSU
      UBYNUUAYDACYDUUABEVTWAWBWCWDVBWEWFVBCDFWGWHVMYMYDXNACXMXPAAGGFXLYLFIAUQXK
      ABGWIWJXPAUQWKYMXHYDXNUMXHYDYMXNXHYDYMXNUMZXHYDKXGXLWLZEJZUUCABCESWMXHUUE
      UUCUMYDYMXHUUEXNXMXPXHUUEXNUMZUMXPXHXMUUFXPXHXMUUFUMZXPXHKXGFWLZDIZUUGCDX
      GFWNUUEXMUUIXNUUEXMUUIXNUMUUEXMKZUUIXNUUJUUHEDXMUUEUUHEJXMUUDUUHEXMUUHUUD
      XGFXLWOWPWQWRWSWTWEXAVBWEXBVMXCUIXDWEXBVMXEXDXF $.
      $( [18-May-2010] $) $( [17-May-2010] $)
  $}

  ${
    $d A x y $.
    $( The restricted identity expressed with the class builder.  NEW $)
    riecb $p |- { <. x , y >. | ( x e. A /\ y = x ) } = ( _I |` A ) $=
      ( weq copab cres cv wcel wa cid resopab wceq equcom opabbii dfid3 eqtr4i
      reseq1 ax-mp eqtr3i ) BADZABEZCFZAGCHTIABEJCFZTABCKUAJLUBUCLUAABDZABEJTUD
      ABBAMNABOPUAJCQRS $.
      $( [21-May-2012] $) $( [25-Apr-2012] $)
  $}

  ${
    $d A x y $.
    $( The restricted identity expressed with the "maps to" notation.  NEW $)
    riemtn $p |- ( x e. A |-> x ) = ( _I |` A ) $=
      ( vy cv cmpt wcel weq wa copab cid cres df-mpt riecb eqtri ) ABADZEOBFCAG
      HACIJBKACBOLACBMN $.
      $( [21-May-2012] $) $( [25-Apr-2012] $)
  $}

  ${
    $d A x $.
    $( Composition of a restricted identity and a mapping (using the maps to
       notation).  See ~ fcoi1 .  NEW $)
    crimmt1 $p |- ( F : A --> B -> ( F o. ( x e. A |-> x ) ) = F ) $=
      ( wf cid cres ccom cv cmpt fcoi1 riemtn coeq2i syl5eq ) BCDEDFBGZHDDABAIJ
      ZHBCDKPODABLMN $.
      $( [21-May-2012] $) $( [25-Apr-2012] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Composition of a restricted identity and a mapping (using the maps to
       notation).  See ~ fcoi2 .  NEW $)
    crimmt2 $p |- ( F : A --> B -> ( ( x e. B |-> x ) o. F ) = F ) $=
      ( wf cid cres ccom cv cmpt fcoi2 riemtn coeq1i syl5eq ) BCDEFCGZDHDACAIJZ
      DHBCDKPODACLMN $.
      $( [21-May-2012] $) $( [25-Apr-2012] $)
  $}

  ${
    $d A y $.  $d B y $.  $d C y $.  $d ph x y $.
    mpteq2dv.1 $e |- ( ( ph /\ x e. A ) -> B = C ) $.
    $( Still another equality inference for the "maps to" notation.  NEW $)
    mpteq2dv $p |- ( ph -> ( x e. A |-> B ) = ( x e. A |-> C ) ) $=
      ( vy cv wcel wceq wa copab cmpt eqeq2d pm5.32da opabbidv df-mpt 3eqtr4g
      ) ABHCIZGHZDJZKZBGLSTEJZKZBGLBCDMBCEMAUBUDBGASUAUCASKDETFNOPBGCDQBGCEQR
      $.
      $( [21-May-2012] $) $( [25-Apr-2012] $)
  $}

  ${
    $d A f x $.  $d A1 f x $.  $d B f x $.  $d B1 f x $.  $d E f x $.
    $d F1 x $.  $d G f x $.
    injsurinj.1 $e |- F1 = ( f e. ( B ^m A ) |-> ( ( E o. f ) o. G ) ) $.
    $( Function returning a composite. $)
    mapmapmap $p |- ( ( E : B --> B1 /\ G : A1 --> A /\
      ( ( A e. _V /\ B e. _V ) /\ ( A1 e. _V /\ B1 e. _V ) ) )
      -> F1 : ( B ^m A ) --> ( B1 ^m A1 ) ) $=
      ( vx cmap co wf wfn cv cfv wcel wral cvv wa w3a ffnfv ccom coexg wi fex
      expcom ad2antlr impcom 3adant2 visset sylancl ad2antrl com12 a1i 3imp
      jca adantr r19.21aiva ralimi syl fopab2ga sylib wceq simpr syl11anc weq
      coeq2 coeq1d fvmptg simpl1 wb pm3.22 3ad2ant3 elmapg biimpa fco simpl2
      syl6 feq1 sylibrd 3syl pm2.43i adantl mpbird sylanbrc ) BAKLZGFKLZHMHWGNZ
      JOZHPZWHQZJWGRBGDMZFAEMZASQZBSQZTZFSQZGSQZTZTZUAZJWGWHHUBXBDCOZUCZEUCZSQZ
      CWGRZWIXBXDSQZESQZTZCWGRXGXBXJCWGXBXJXCWGQXBXHXIDSQZXCSQXHXBDXCSSUDWMXAXK
      WNXAWMXKWPWMXKUEWOWTWMWPXKBGSDUFUGUHUIUJZCUKULWMWNXAXIWNXAXIUEUEWMXAWNXIW
      RWNXIUEWQWSWNWRXIFASEUFUGUMUNUOUPZUQURUSXJXFCWGXDESSUDUTVACWGXEHIVBVCXBWL
      JWGXBWJWGQZTZWLFGWKMZXOXPXOXNDWJUCZEUCZSQZTWKXRVDZXOXPUEXOXNXSXBXNVEXOXQS
      QZXIXSXKWJSQYAXODWJSSUDXBXKXNXLURJUKULXBXIXNXMURXQESSUDVFUQCWJXEXRWGSHCJV
      GXDXQEXCWJDVHVIIVJXTXOFGXRMZXPXTXOAGXQMZWNTZYBXOYDUEXTXOYCWNXOWMABWJMZYCW
      MWNXAXNVKXBXNYEXBWPWOTZXNYEVLXAWMYFWNWQYFWTWOWPVMURVNBAWJSSVOVAVPABGDWJVQ
      VFWMWNXAXNVRUQUOFAGXQEVQVSFGWKXRVTWAWBWCXOWSWRTZWLXPVLXBYGXNXAWMYGWNWTYGW
      QWRWSVMWDVNURGFWKSSVOVAWEUSWF $.
      $( [22-Nov-2011] $) $( [19-Nov-2011] $)
  $}

  ${
    $d A f x y $.  $d A1 f x y $.  $d B f x y $.  $d B1 f x y $.  $d E f x y $.
    $d F1 x y $.  $d G f x y $.
    injsuinj.1 $e |- F1 = ( f e. ( B ^m A ) |-> ( ( E o. f ) o. G ) ) $.
    $( If ` E ` is an injection and ` G ` a surjection
       ` ( f |-> ( ( E o. f ) o. G ) ) ` is an injection.  Bourbaki E.II.31
       prop. 2. $)
    injsurinj $p |- ( ( E : B -1-1-> B1 /\ G : A1 -onto-> A
       /\ ( ( A e. _V /\ B e. _V ) /\ ( A1 e. _V /\ B1 e. _V ) ) )
        -> F1 : ( B ^m A ) -1-1-> ( B1 ^m A1 ) ) $=
      ( vx vy cmap co wf1 wf cv cfv wceq weq wi wral wfo cvv wcel wa w3a dff13
      mapmapmap f1f fof id syl3an ccom simp1 coexg adantr simprlr fex syl11anc
      3adant2 visset sylancl 3ad2ant2 simp3rl 3ad2ant3 jca coeq2 coeq1d fvmptg
      simp2 eqtr simpr31 wb elmapg ancoms biimpd impcom adantl 3adant1 3jca
      simp32 3ad2ant1 fco wfn surrc2 ffn syl111anc njtlc sylc ex syl eqcoms
      com4t 3syl pm2.43i com12 3expia r19.21aivv sylanbrc ) BALMZGFLMZHNWTXAHOZ
      JPZHQZKPZHQZRZJKSZTZKWTUAJWTUABGDNZFAEUBZAUCUDZBUCUDZUEZFUCUDZGUCUDZUEZUE
      ZUFZJKWTXAHUGBGDOZFAEOZXRXBXJXKXRABCDEFGHIUHBGDUIZFAEUJZXRUKULXSXIJKWTWTX
      CWTUDZXEWTUDZUEXSXIYDYEXSXIYDYEXSUFZXIYFYDDXCUMZEUMZUCUDZUEXDYHRZYFXITYFY
      DYIYDYEXSUNXSYDYIYEXSYGUCUDZEUCUDZYIDUCUDZXCUCUDYKXSDXCUCUCUOXJXRYMXKXJXR
      UEXTXMYMXJXTXRYBUPXJXLXMXQUQBGUCDURUSUTZJVAVBXSYAXOYLXKXJYAXRYCVCXOXPXNXJ
      XKVDFAUCEURUSZYGEUCUCUOUSVEVFCXCDCPZUMZEUMZYHWTUCHCJSYQYGEYPXCDVGVHIVIYFY
      JXIYFYJXITZYFYEDXEUMZEUMZUCUDZUEXFUUARZYFYSTYFYEUUBYDYEXSVJYFYTUCUDZYLUUB
      YMXEUCUDUUDYFDXEUCUCUOXSYDYMYEYNVEKVAVBXSYDYLYEYOVEYTEUCUCUOUSVFCXEYRUUAW
      TUCHCKSYQYTEYPXEDVGVHIVIYJXGUUCYFXHXGUUCYFXHTZTZTYHXDYHXDRZXGUUFUUGXGUEYH
      XFRZUUFYHXDXFVKUUHUUCUUEUUHUUCUEYHUUARZUUEYHXFUUAVKUUIYFXHUUIYFUEZXJABXCO
      ZABXEOZUFYGYTRZXHUUJXJUUKUULXJXKXRYDYEUUIVLYFUUKUUIYDXSUUKYEXSYDUUKXRXJYD
      UUKTZXKXNUUNXQXNYDUUKXMXLYDUUKVMBAXCUCUCVNVOVPUPVEVQUTZVRYFUULUUIYEXSUULY
      DXSYEUULXRXJYEUULTZXKXNUUPXQXNYEUULXMXLYEUULVMBAXEUCUCVNVOVPUPVEVQVSZVRVT
      YFUUIUUMYFXKAGYGOZAGYTOZUUIUUMTYDYEXJXKXRWAYFXTUUKUURXSYDXTYEXJXKXTXRYBWB
      VEZUUOABGDXCWCUSYFXTUULUUSUUTUUQABGDXEWCUSXKUURUUSUFUUIUUMXKYGAWDYTAWDUUI
      UUMVMXKUURUUSFAEYGYTWEXKUKAGYGWFAGYTWFULVPWGVQABGDXCXEWHWIWJWKWJWKWJWLWMW
      NWOWPWNWOWQWPWRWS $.
      $( [22-Nov-2011] $) $( [20-Nov-2011] $)
  $}

  $( The ordered pair ` <. A , A >. ` in Kuratowski's representation is a
     singleton of singleton. $)
  orkurss $p |- <. A , A >. = { { A } } $=
    ( csn cpr cop wceq dfsn2 eqcomi preq2 ax-mp df-op 3eqtr4i ) ABZAACZCZLLCZAA
    DLBMLENOELMAFGMLLHIAAJLFK $.
    $( [16-Apr-2012] $) $( [28-Dec-2011] $)

