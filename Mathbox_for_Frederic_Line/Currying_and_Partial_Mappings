$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Currying and Partial Mappings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c cur1 $.
  $c cur2 $.

  $( Extend class notation with the definition of currying. $)
  ccur1 $a class cur1 $.

  $( Extend class notation with the definition of currying. $)
  ccur2 $a class cur2 $.

  ${
    $d f g x y $.
    $( Definition of currying (1st sort).  Currying the operation ` f `
       consists in creating a mapping that returns for every value ` x ` of
       ` dom dom f ` the partial application of ` f ` to ` x ` . $)
    df-cur1 $a |- cur1 = { <. f , g >. | ( ( Fun f /\ Rel dom f ) /\
   g = { <. x , y >. | ( x e. dom dom f
     /\ y = ( f o. `' ( 2nd |` ( { x } X. _V ) ) ) ) } ) } $.


    $( Definition of currying (2nd sort).  Currying the operation ` f `
       consists in creating a mapping that returns for every value ` x ` of
       ` ran dom f ` the partial application of ` f ` to ` x ` . $)
    df-cur2 $a |- cur2 = { <. f , g >. | ( Fun f /\ Rel dom f /\
   g = { <. x , y >. | ( x e. ran dom f
     /\ y = ( f o. `' ( 1st |` ( _V X. { x } ) ) ) ) } ) } $.
  $}

  ${
    $d F f g x y $.
    $( The value of a curried operation.  (Contributed by FL, 24-Jan-2010.) $)
    cur1val $p |- ( ( F e. A /\ Fun F /\ Rel dom F ) -> ( cur1 ` F )
      = { <. x , y >. | ( x e. dom dom F /\
        y = ( F o. `' ( 2nd |` ( { x } X. _V ) ) ) ) } ) $=
      ( vf vg wcel wfun cdm wrel cvv cv ccom wceq wa copab ccur1 3ad2ant1 dmexg
      syl w3a c2nd csn cxp cres ccnv elisset opabex2g 3simpc df-cur1 funeq dmeq
      cfv wb releq anbi12d dmeqd eleq2d coeq1 eqeq2d opabbidv fvopab6 syl111anc
      ) DCGZDHZDIZJZUAZDKGZALZVFIZGZBLZDUBVJUCKUDUEUFZMZNZOZABPZKGZVEVGOZDQUMVR
      NVDVEVIVGDCUGRVHVKKGZVSVDVEWAVGVDVFKGWADCSVFKSTRABVKVOKUHTVDVEVGUIELZHZWB
      IZJZOVTEFDVJWDIZGZVMWBVNMZNZOZABPVRKKQABEFUJWBDNZWCVEWEVGWBDUKWKWDVFNWEVG
      UNWBDULZWDVFUOTUPWKWJVQABWKWGVLWIVPWKWFVKVJWKWDVFWLUQURWKWHVOVMWBDVNUSUTU
      PVAVBVC $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.  $d F x y $.
    $( The value of a curried operation.  (Contributed by FL, 17-May-2010.) $)
    cur1vald $p |- (
      ( ( F Fn ( A X. B ) /\ B =/= (/) ) /\ ( A e. C /\ B e. D ) )
        -> ( cur1 ` F ) = { <. x , y >. | ( x e. A /\
          y = ( F o. `' ( 2nd |` ( { x } X. _V ) ) ) ) } ) $=
      ( cxp wa wcel cv cdm cvv wceq copab wrel wi adantr imp syl wfn c0 wne cfv
      ccur1 c2nd csn cres ccnv ccom wfun w3a simp1 3adant1 fnex syl11anc 3expib
      xpexg fnfun ad2antrr fndm relxp releq eqcoms mpbii cur1val syl111anc dmeq
      wb dmxp eqeq1d biimpcd eleq2d anbi1d opabbidv eqtr4d ) GCDHZUAZDUBUCZIZCE
      JZDFJZIZIZGUEUDZAKZGLZLZJZBKGUFWFUGMHUHUIUJNZIZABOZWFCJZWJIZABOWDGMJZGUKZ
      WGPZWEWLNVTWCWOVRWCWOQVSVRWAWBWOVRWAWBULVRVQMJZWOVRWAWBUMWAWBWRVRCDEFURUN
      VQMGUOUPUQRSVRWPVSWCVQGUSUTVRWQVSWCVRWGVQNZWQVQGVAZWSVQPZWQCDVBXAWQVIVQWG
      VQWGVCVDVETUTABMGVFVGWDWNWKABWDWMWIWJWDCWHWFVTCWHNZWCVRVSXBVRWSVSXBQZWTWS
      WHVQLZNXCWGVQVHXCXDWHVSXDWHNXBVSXDCWHCDVJVKVLVDTTSRVMVNVOVP $.
      $( [17-May-2010] $)
  $}

  ${
    $d A x y $.  $d B f x y $.  $d C x y $.  $d D x y $.  $d F f x y $.
    domrancur1b.1 $e |- A e. C $.
    domrancur1b.2 $e |- B e. D $.
    domrancur1b.3 $e |- B =/= (/) $.
    domrancur1b.4 $e |- F Fn ( A X. B ) $.
    $( The currying of a mapping ` F ` whose domain is ` ( A X. B ) ` is a
       mapping whose domain is ` A ` and the range, the class of all the
       functions from ` B ` to ` ran F ` .  (Contributed by FL,
       28-Apr-2010.) $)
    domrancur1b $p |-
      ( cur1 ` F ) : A --> { f | f : B --> ran F } $=
      ( vx c2nd cvv crn wcel wa wceq ax-mp a1i eqtrd vy cv csn cxp cres ccnv wf
      ccom cab wral ccur1 cfv wfn wss wfun cdm wf1o 2ndconst wfo dff1o3 simprbi
      fnfun funco syl11anc cima dmco imacnvcnv fndm imaeq2i df-ima resres rneqi
      syl cin inxp visset snss biimpi df-ss sylib incom inv1 eqtri xpeq12 rneqd
      reseq2d c0 wne snnzg fo2ndres forn jca df-fn sylibr rncoss elisset adantl
      df-f cpm co pm3.2i xpexg mpbird funrnex imp fpm feq1 eqid elab2g pm5.21nd
      eleq1 rgen copab cur1vald fopab2 mpbi ) FLKUBZUCZMUDZUEZUFZUHZBFNZEUBZUGZ
      EUIZOZKAUJAYFFUKULZUGYGKAXQAOZYGBYCYBUGZYIYBBUMZYBNYCUNZPYJYIYKYLYIYBUOZY
      BUPZBQZPYKYIYMYOYIFUOZYAUOZYMYPYIFABUDZUMZYPJYRFVBRZSYIXSMXTUQZYQXQMAURUU
      AXSMXTUSYQXSMXTUTVAVMFYAVCVDYIYNYAUFFUPZVEZBYNUUCQYIFYAVFSYIUUCXTUUBVEZBU
      UCUUDQYIXTUUBVGSYIUUDXTYRVEZBUUDUUEQYIUUBYRXTYSUUBYRQZJYRFVHRZVISYIUUEXTY
      RUEZNZBUUEUUIQYIXTYRVJSYIUUILXSYRVNZUEZNZBUUIUULQYIUUHUUKLXSYRVKVLSYIUULL
      XRBUDZUEZNZBYIUUKUUNYIUUJUUMLYIUUJXRAVNZMBVNZUDZUUMUUJUURQYIXRMABVOSYIUUP
      XRQZUUQBQZUURUUMQYIXRAUNZUUSYIUVAXQAKVPVQVRXRAVSVTUUTYIUUQBMVNBMBWABWBWCS
      UUPXRUUQBWDVDTWFWEYIUUMBUUNUSZUUOBQYIXRWGWHUVBXQAWIXRBWJVMUUMBUUNWKVMTTTT
      TTWLYBBWMWNYLYIFYAWOSWLBYCYBWRWNYIYGYJYBMOZYGUVCYIYBYFWPWQYJUVCYIYJYBYCBW
      SWTZOUVCBYCYBBDOZBMOHBDWPRUUBMOZYPPYCMOZUVFYPUUFUVFUUGUUFUVFYRMOZUVHUUFAC
      OZUVEPZUVHUVIUVEGHXAZABCDXBRSUUBYRMXKXCRYTXAUVFYPUVGMFXDXERXFYBUVDWPVMWQY
      EYJEYBYFMBYCYDYBXGYFXHXIXJXCXLKUAAYFYBYHYSBWGWHZPZUVJPYHYIUAUBYBQPKUAXMQU
      VMUVJYSUVLJIXAUVKXAKUAABCDFXNRXOXP $.
      $( [28-Apr-2010] $)
  $}

  ${
    $( Lemma for ~ domrancur1c . $)
    domrancur1clem $p |- ( ( F Fn ( A X. B ) /\ ( A e. C /\ B e. D ) )
       -> ( F o. `' ( 2nd |` M ) ) e. _V ) $=
      ( cxp wfn wcel wa cvv c2nd cres ccnv wfun ccom xpexg fnex sylan2 ax-mp
      wfo fo2nd fofun funres funcnvcnv cofunex2g sylancl ) EABGZHZACIBDIJZJEKIZ
      LFMZNZNOZEUMPKIUJUIUHKIUKABCDQUHKERSULOZUNLOZUOKKLUAUPUBKKLUCTFLUDTULUETE
      UMKUFUG $.
      $( [17-May-2010] $)
  $}

  ${
    $d A x y $.  $d B f x y $.  $d C x y $.  $d D x y $.  $d F f x y $.
    $( The currying of a mapping ` F ` whose domain is ` ( A X. B ) ` is a
       mapping whose domain is ` A ` and the range, the class of all the
       functions from ` B ` to ` ran F ` .  (Contributed by FL,
       17-May-2010.) $)
    domrancur1c $p |-
      ( ( ( A e. C /\ B e. D ) /\ ( B =/= (/) /\ F Fn ( A X. B ) ) ) ->
        ( cur1 ` F ) : A --> { f | f : B --> ran F } ) $=
      ( vx wcel wa c2nd cvv cres crn wfun wceq wi ax-mp syl a1i cin wne cxp wfn
      vy c0 cv csn ccnv ccom wf cab wral ccur1 cfv wss cdm wf1o visset 2ndconst
      f1ocnv f1ofun fnfun funco ex ad2antlr syl5com cima dmco cnvcnvres imaeq1i
      adantl df-ima resres rneqd rescnvcnv fndm simpr ineq2d reseq2d inxp rneqi
      reseq2i snssi df-ss sylib incom inv1 eqtri xpeq12 sylancl ad2antrr pm2.27
      wfo f1ofo syl6 mp2 forn 3eqtrd eqtrd exp31 com13 imp df-fn sylanbrc dffn3
      impcom rncoss fss xpexg fnex syl5 adantr fo2nd funres funcnvcnv cofunex2g
      wb fofun syl11anc elabg mpbird r19.21aiva copab pm3.22 simpl jca cur1vald
      feq1 fopab2g 3syl mpbid ) ACHBDHIZBUEUAZFABUBZUCZIZIZFJGUFZUGZKUBZLZUHZUI
      ZBFMZEUFZUJZEUKZHZGAULZAUUGFUMUNZUJZYQUUHGAYQYRAHZIZUUHBUUDUUCUJZUUMBUUCM
      ZUUCUJZUUOUUDUOUUNUUMUUCBUCZUUPUUMUUCNZUUCUPZBOUUQKYTUUBUQZUUMUURPYTKUUAU
      QZUUTYRKHZUVAGURZYRKKUSQYTKUUAUTQUUTUUBNZUUMUURKYTUUBVAYPUVDUURPZYLUULYOU
      VEYMYOFNZUVEYNFVBUVFUVDUURFUUBVCVDRVKVEVFQUUMUUSUUBUHZFUPZVGZJUHUHZYTLZUV
      HVGZBUUSUVIOUUMFUUBVHSUVIUVLOUUMUVGUVKUVHJYTVIVJSUUMUVLUVKUVHLZMZUVJYTUVH
      TZLZMZBUVLUVNOUUMUVKUVHVLSUUMUVMUVPUVMUVPOUUMUVJYTUVHVMSVNUUMUVQJUVOLZMZB
      UUMUVPUVRUVPUVROUUMJUVOVOSVNYQUULUVSBOZYPYLUULUVTPZYOYLUWAPZYMYOUVHYNOZUW
      BYNFVPUULYLUWCUVTUULYLUWCUVTUULYLIZUWCIZUVSJYTYNTZLZMZBUWEUVRUWGUWEUVOUWF
      JUWEUVHYNYTUWDUWCVQVRVSVNUWEUWHJYSATZKBTZUBZLZMZJYSBUBZLZMZBUWHUWMOUWEUWG
      UWLUWFUWKJYSKABVTWBWASUULUWMUWPOYLUWCUULUWLUWOUULUWKUWNJUULUWIYSOZUWJBOUW
      KUWNOUULYSAUOUWQYRAWCYSAWDWEUWJBKTBKBWFBWGWHUWIYSUWJBWIWJVSVNWKUWEUWNBUWO
      WMZUWPBOUWRUWEUVBUVBUWNBUWOUQZPZUWRUVCYRBKUSUVBUWTUWSUWRUVBUWSWLUWNBUWOWN
      WOWPSUWNBUWOWQRWRWSWTXARVKXFXBWSWRWRUUCBXCXDBUUCXEWEFUUBXGBUUOUUDUUCXHWJU
      UMUUCKHZUUHUUNXQUUMFKHZUVGNZUXAYQUXBUULYPYLUXBYOYLUXBPYMYLYNKHZYOUXBABCDX
      IYOUXDUXBYNKFXJVDXKVKXFXLUUMUUANZUXCUXEUUMJNZUXEKKJWMUXFXMKKJXRQYTJXNQSUU
      AXORFUUBKXPXSUUFUUNEUUCKBUUDUUEUUCYHXTRYAYBYQYOYMIZYLIUUJUULUDUFUUCOIGUDY
      COUUIUUKXQYQUXGYLYPUXGYLYMYOYDVKYLYPYEYFGUDABCDFYGGUDAUUGUUCUUJYIYJYK $.
      $( [17-May-2010] $)
  $}

  ${
    $d B f $.  $d F f $.  $d O f $.
    $( The value of a curried function at ` O e. A ` is a mapping.
       (Contributed by FL, 17-May-2010.) $)
    valcurfn $p |- ( ( ( A e. C /\ B e. D ) /\
               ( B =/= (/) /\ F Fn ( A X. B ) ) /\ O e. A )
        ->
        ( ( cur1 ` F ) ` O ) : B --> ran F ) $=
      ( vf wcel wa c0 wne cxp wfn crn ccur1 cfv wf cv cab wi ffvelrn fvex sylib
      domrancur1c feq1 elab ex syl 3impia ) ACHBDHIZBJKEABLMIZFAHZBENZFEOPZPZQZ
      UJUKIABUMGRZQZGSZUNQZULUPTABCDGEUDUTULUPUTULIUOUSHUPAUSFUNUAURUPGUOFUNUBB
      UMUQUOUEUFUCUGUHUI $.
      $( [17-May-2010] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d O x y $.
    valcurfn1.1 $e |- G = ( F o. `' ( 2nd |` ( { O } X. _V ) ) ) $.
    $( The value of a curried function at ` O e. A ` is a partial application.
       (Contributed by FL, 17-May-2010.) $)
    valcurfn1 $p |- ( ( ( A e. C /\ B e. D ) /\
               ( B =/= (/) /\ F Fn ( A X. B ) ) /\ O e. A )
        ->
        ( ( cur1 ` F ) ` O ) = G ) $=
      ( vx vy wcel wa cxp cfv cdm c2nd cvv wceq adantl syl c0 wne wfn w3a ccur1
      cv csn cres ccnv ccom copab wfun wrel wi xpexg fnex ex syl5 3adant3 fnfun
      impcom 3ad2ant2 fndm relxp mpbiri cur1val syl111anc fveq1d dmeq dmxp eqtr
      releq eqcomd syl2im eleq2d biimpa 3adant1 simprr simpl jca domrancur1clem
      syl5eqel sneq xpeq1 reseq2d cnveq coeq2d syl6eqr fvopab4g syl11anc eqtrd
      eqid ) ACKBDKLZBUAUBZEABMZUCZLZGAKZUDZGEUENZNGIUFZEOZOZKJUFEPXAUGZQMZUHZU
      IZUJZRLIJUKZNZFWSGWTXIWSEQKZEULZXBUMZWTXIRWMWQXKWRWQWMXKWPWMXKUNWNWMWOQKZ
      WPXKABCDUOWPXNXKWOQEUPUQURSVAUSWQWMXLWRWPXLWNWOEUTSVBWQWMXMWRWPXMWNWPXBWO
      RZXMWOEVCZXOXMWOUMABVDXBWOVLVETSVBIJQEVFVGVHWSGXCKZFQKXJFRWQWRXQWMWQWRXQW
      QAXCGWPWNAXCRZWPXOWNXRUNXPXOXCWOOZRZWNXSARZXRXBWOVIABVJXTYAXRXTYALXCAXCXS
      AVKVMUQVNTVAVOVPVQWSFEPGUGZQMZUHZUIZUJZQHWSWPWMLZYFQKWMWQYGWRWMWQLWPWMWMW
      NWPVRWMWQVSVTUSABCDEYCWATWBIJGXHFXCQXIXAGRZXHYFFYHXGYEEYHXFYDRXGYERYHXEYC
      PYHXDYBRXEYCRXAGWCXDYBQWDTWEXFYDWFTWGHWHXIWLWIWJWK $.
      $( [17-May-2010] $)

  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d O x y $.
    $( The value of a curried function at ` O e. A ` is a partial application.
       (Contributed by FL, 17-May-2010.) $)
    valcurfn2 $p |- ( ( ( A e. C /\ B e. D ) /\
               ( B =/= (/) /\ F Fn ( A X. B ) ) /\ O e. A )
        ->
        ( ( cur1 ` F ) ` O ) =
        { <. x , y >. | ( x e. B /\ y = ( O F x ) ) } ) $=
      ( wcel wa c0 wne cxp wfn w3a ccur1 cfv c2nd cv wceq csn cres ccnv ccom co
      cvv copab eqid valcurfn1 simp2r simp3 curry1 syl11anc eqtrd ) CEIDFIJZDKL
      ZGCDMNZJZHCIZOZHGPQQGRHUAUFMUBUCUDZASZDIBSHVBGUETJABUGZCDEFGVAHVAUHZUIUTU
      QUSVAVCTUOUPUQUSUJUOURUSUKABCDHGVAVDULUMUN $.
      $( [17-May-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d O x y $.  $d P x y $.
    $( The value at ` P e. B ` of the value of a curried function at ` O e. A `
       equals ` ( O F P ) ` .  (Contributed by FL, 17-May-2010.) $)
    valvalcurfn $p |- ( ( ( A e. C /\ B e. D ) /\
               ( B =/= (/) /\ F Fn ( A X. B ) ) /\ ( O e. A /\ P e. B ) )
        ->
        ( ( ( cur1 ` F ) ` O ) ` P ) = ( O F P ) ) $=
      ( vx vy wcel wa c0 wne cxp wfn cfv cv co wceq cvv w3a ccur1 fveq1d simp3r
      copab valcurfn2 3adant3r oprex opreq2 eqid fvopab4g sylancl eqtrd ) ACJBD
      JKZBLMFABNOKZGAJZEBJZKUAZEGFUBPPZPEHQZBJIQGUTFRZSKHIUEZPZGEFRZUREUSVBUNUO
      UPUSVBSUQHIABCDFGUFUGUCURUQVDTJVCVDSUNUOUPUQUDGEFUHHIEVAVDBTVBUTEGFUIVBUJ
      UKULUM $.
      $( [17-May-2010] $)
  $}


