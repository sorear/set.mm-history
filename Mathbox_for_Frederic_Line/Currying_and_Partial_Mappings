$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Currying and Partial Mappings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c cur1 $.
  $c cur2 $.

  $( Extend class notation with the definition of currying. $)
  ccur1 $a class cur1 $.

  $( Extend class notation with the definition of currying. $)
  ccur2 $a class cur2 $.

  ${
    $d f g x y $.
    $( Definition of currying (1st sort).  Currying the operation ` f `
       consists in creating a mapping that returns for every value ` x ` of
       ` dom dom f ` the partial application of ` f ` to ` x ` . $)
    df-cur1 $a |- cur1 = { <. f , g >. | ( ( Fun f /\ Rel dom f ) /\
   g = { <. x , y >. | ( x e. dom dom f
     /\ y = ( f o. `' ( 2nd |` ( { x } X. _V ) ) ) ) } ) } $.


    $( Definition of currying (2nd sort).  Currying the operation ` f `
       consists in creating a mapping that returns for every value ` x ` of
       ` ran dom f ` the partial application of ` f ` to ` x ` . $)
    df-cur2 $a |- cur2 = { <. f , g >. | ( Fun f /\ Rel dom f /\
   g = { <. x , y >. | ( x e. ran dom f
     /\ y = ( f o. `' ( 1st |` ( _V X. { x } ) ) ) ) } ) } $.
  $}

  ${
    $d F f g x y $.
    $( The value of a curried operation. $)
    cur1val $p |- ( ( F e. A /\ Fun F /\ Rel dom F ) -> ( cur1 ` F )
      = { <. x , y >. | ( x e. dom dom F /\
        y = ( F o. `' ( 2nd |` ( { x } X. _V ) ) ) ) } ) $=
      ( vf vg cvv wcel cv cdm c2nd csn cxp cres ccnv ccom wceq wa copab wfun
      wrel ccur1 cfv w3a df-cur1 funeq wb dmeq releq syl anbi12d dmeqd eleq2d
      coeq1 eqeq2d opabbidv fvopab6 elisset 3ad2ant1 dmexg opabex2g 3simpc
      syl3anc ) DGHZAIZDJZJZHZBIZDKVELGMNOZPZQZRZABSZGHZDTZVFUAZRZDUBUCVNQDCHZV
      PVQUDZEIZTZWAJZUAZRVREFDVEWCJZHZVIWAVJPZQZRZABSVNGGUBABEFUEWADQZWBVPWDVQW
      ADUFWJWCVFQWDVQUGWADUHZWCVFUIUJUKWJWIVMABWJWFVHWHVLWJWEVGVEWJWCVFWKULUMWJ
      WGVKVIWADVJUNUOUKUPUQVSVPVDVQDCURUSVTVGGHZVOVSVPWLVQVSVFGHWLDCUTVFGUTUJUS
      ABVGVKGVAUJVSVPVQVBVC $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.  $d F x y $.
    $( The value of a curried operation. $)
    cur1vald $p |- (
      ( ( F Fn ( A X. B ) /\ B =/= (/) ) /\ ( A e. C /\ B e. D ) )
        -> ( cur1 ` F ) = { <. x , y >. | ( x e. A /\
          y = ( F o. `' ( 2nd |` ( { x } X. _V ) ) ) ) } ) $=
      ( cxp wfn c0 wne wa wcel ccur1 cfv cv cdm c2nd csn cvv cres ccnv ccom
      wceq copab wfun wrel cur1val wi w3a fnex 3simp1 xpexg 3adant1 sylanc
      3expib adantr imp fnfun ad2antrr fndm relxp wb releq eqcoms mpbii syl
      syl3anc dmeq dmxp eqeq1d biimpcd eleq2d anbi1d opabbidv eqtr4d ) GCDHZIZD
      JKZLZCEMZDFMZLZLZGNOZAPZGQZQZMZBPGRWFSTHUAUBUCUDZLZABUEZWFCMZWJLZABUEGTMZ
      GUFZWGUGZWEWLUDWDABTGUHVTWCWOVRWCWOUIVSVRWAWBWOVRVQTMZWOVRWAWBUJVQTGUKVRW
      AWBULWAWBWRVRCDEFUMUNUOUPUQURVRWPVSWCVQGUSUTVRWQVSWCVRWGVQUDZWQVQGVAZWSVQ
      UGZWQCDVBXAWQVCVQWGVQWGVDVEVFVGUTVHWDWNWKABWDWMWIWJWDCWHWFVTCWHUDZWCVRVSX
      BVRWSVSXBUIZWTWSWHVQQZUDXCWGVQVIXCXDWHVSXDWHUDXBVSXDCWHCDVJVKVLVEVGVGURUQ
      VMVNVOVP $.
      $( [18-May-2010] $) $( [17-May-2010] $)
  $}

  ${
    $d A x y $.  $d B f x y $.  $d C x y $.  $d D x y $.  $d F f x y $.
    domrancur1b.1 $e |- A e. C $.
    domrancur1b.2 $e |- B e. D $.
    domrancur1b.3 $e |- B =/= (/) $.
    domrancur1b.4 $e |- F Fn ( A X. B ) $.
    $( The currying of a mapping ` F ` whose domain is ` ( A X. B ) ` is a
       mapping whose domain is ` A ` and the range, the class of all the
       functions from ` B ` to ` ran F ` . $)
    domrancur1b $p |-
      ( cur1 ` F ) : A --> { f | f : B --> ran F } $=
      ( vx vy c2nd cv csn cvv cxp cres ccnv ccom crn wf cab wcel wral ccur1
      cfv wfn wss wa wfun cdm wceq fnfun ax-mp a1i wf1o 2ndconst wfo dff1o3
      pm3.27bi syl jca funco cima dmco imacnvcnv fndm imaeq2i df-ima cin
      resres rneqi inxp visset snss biimpi df-ss sylib incom inv1 eqtri xpeq12
      eqtrd reseq2 rneqd c0 wne snnzg fo2ndres forn df-fn sylibr rncoss df-f
      elisset adantl cpm co pm3.2i xpexg eleq1 mpbird funrnex imp fpm feq1
      eqid elab2g pm5.21nd rgen copab cur1vald fopab2 mpbi ) FMKNZOZPQZRZSZTZBF
      UAZENZUBZEUCZUDZKAUEAYEFUFUGZUBYFKAXPAUDZYFBYBYAUBZYHYABUHZYAUAYBUIZUJYIY
      HYJYKYHYAUKZYAULZBUMZUJYJYHYLYNYHFUKZXTUKZUJYLYHYOYPYOYHFABQZUHZYOJYQFUNU
      OZUPYHXRPXSUQZYPXPPAURYTXRPXSUSYPXRPXSUTVAVBVCFXTVDVBYHYMXTSFULZVEZBYMUUB
      UMYHFXTVFUPYHUUBXSUUAVEZBUUBUUCUMYHXSUUAVGUPYHUUCXSYQVEZBUUCUUDUMYHUUAYQX
      SYRUUAYQUMZJYQFVHUOZVIUPYHUUDXSYQRZUAZBUUDUUHUMYHXSYQVJUPYHUUHMXRYQVKZRZU
      AZBUUHUUKUMYHUUGUUJMXRYQVLVMUPYHUUKMXQBQZRZUAZBYHUUJUUMYHUUIUULUMUUJUUMUM
      YHUUIXQAVKZPBVKZQZUULUUIUUQUMYHXQPABVNUPYHUUOXQUMZUUPBUMZUJUUQUULUMYHUURU
      USYHXQAUIZUURYHUUTXPAKVOVPVQXQAVRVSUUSYHUUPBPVKBPBVTBWAWBUPVCUUOXQUUPBWCV
      BWDUUIUULMWEVBWFYHUULBUUMUSZUUNBUMYHXQWGWHUVAXPAWIXQBWJVBUULBUUMWKVBWDWDW
      DWDWDWDVCYABWLWMYKYHFXTWNUPVCBYBYAWOWMYHYFYIYAPUDZYFUVBYHYAYEWPWQYIUVBYHY
      IYAYBBWRWSZUDUVBBYBYABDUDZBPUDHBDWPUOUUAPUDZYOUJYBPUDZUVEYOUUEUVEUUFUUEUV
      EYQPUDZUVGUUEACUDZUVDUJZUVGUVHUVDGHWTZABCDXAUOUPUUAYQPXBXCUOYSWTUVEYOUVFP
      FXDXEUOXFYAUVCWPVBWQYDYIEYAYEPBYBYCYAXGYEXHXIXJXCXKKLAYEYAYGYRBWGWHZUJZUV
      IUJYGYHLNYAUMUJKLXLUMUVLUVIYRUVKJIWTUVJWTKLABCDFXMUOXNXO $.
      $( [18-May-2010] $) $( [28-Apr-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C y z $.  $d F x z $.
    $( Functionality of an ordered-pair class abstraction. $)
    fopab2g $p |- ( F = { <. x , y >. | ( x e. A /\ y = C ) } ->
       ( A. x e. A C e. B <-> F : A --> B ) ) $=
      ( vz cv wcel wceq wa copab wral wf wfn crn wss weu cvv elisset eueq
      sylib r19.20si adantl eqid fnopabg wb fneq1 adantr mpbird rneq sseq1d wi
      wal wex hbra1 ax-17 ra4 eleq1a imim2i imp3a syl 19.23ad rnopab abeq2i
      syl5ib 19.21aiv hbopab2 hbrn dfss2f sylibr syl5bir imp jca df-f cdm dmeq
      eqtr dmopab3 isset bicomi ralbii biimpi sylbir ex eqcoms fdm syl5
      hbopab1 hbeq hban cfv fvopab2 ffvelrn fveq1 eqcomd eqeq1d biimpac eleq1d
      biimpd com13 com3r com3l mpd r19.20da impbida ) FAHZCIZBHZEJZKZABLZJZEDIZ
      ACMZCDFNZXMXOKZFCOZFPZDQZKXPXQXRXTXQXRXLCOZXQXJBRZACMZYAXOYCXMXNYBACXNESI
      ZYBEDTBEUAUBUCUDXJABCXLXLUEUFUBXMXRYAUGXOCFXLUHUIUJXMXOXTXMXTXLPZDQZXOXMX
      SYEDFXLUKULXOXIYEIZXIDIZUMZBUNYFXOYIBXOXKAUOZYHYGXOXKYHAXNACUPYHAUQXOXHXN
      UMZXKYHUMXNACURYKXHXJYHXNXJYHUMXHEDXIUSUTVAVBVCYJBYEXKABVDVEVFVGBGYEDBGXL
      XKABGVHVIGHZDIBUQVJVKVLVMVNCDFVOVKXMXPKZYDACMZXOXMXPYNXMFVPZCJZYNXPXMYOXL
      VPZJYPYNUMZFXLVQYRYQYOYQYOJZYPYNYSYPKYQCJZYNYQYOCVRYTXJBUOZACMZYNXJABCVSU
      UBYNUUAYDACYDUUABEVTWAWBWCWDVBWEWFVBCDFWGWHVMYMYDXNACXMXPAAGGFXLYLFIAUQXK
      ABGWIWJXPAUQWKYMXHYDXNUMXHYDYMXNXHYDYMXNUMZXHYDKXGXLWLZEJZUUCABCESWMXHUUE
      UUCUMYDYMXHUUEXNXMXPXHUUEXNUMZUMXPXHXMUUFXPXHXMUUFUMZXPXHKXGFWLZDIZUUGCDX
      GFWNUUEXMUUIXNUUEXMUUIXNUMUUEXMKZUUIXNUUJUUHEDXMUUEUUHEJXMUUDUUHEXMUUHUUD
      XGFXLWOWPWQWRWSWTWEXAVBWEXBVMXCUIXDWEXBVMXEXDXF $.
      $( [18-May-2010] $) $( [17-May-2010] $)
  $}

  ${
    $( Lemma for ~ domrancur1c . $)
    domrancur1clem $p |- ( ( F Fn ( A X. B ) /\ ( A e. C /\ B e. D ) )
       -> ( F o. `' ( 2nd |` M ) ) e. _V ) $=
      ( cvv wcel c2nd cres ccnv wfun ccom cxp wfn wa cofunex2g fnex xpexg
      sylan2 wfo fo2nd fofun ax-mp funres funcnvcnv sylancl ) EGHZIFJZKZKLZEUJM
      GHEABNZOZACHBDHPZPEUJGQUMULGHUHUNULGERABCDSTUILZUKILZUOGGIUAUPUBGGIUCUDFI
      UEUDUIUFUDUG $.
      $( [18-May-2010] $) $( [17-May-2010] $)
  $}

  ${
    $d A x y $.  $d B f x y $.  $d C x y $.  $d D x y $.  $d F f x y $.
    $( The currying of a mapping ` F ` whose domain is ` ( A X. B ) ` is a
       mapping whose domain is ` A ` and the range, the class of all the
       functions from ` B ` to ` ran F ` . $)
    domrancur1c $p |-
      ( ( ( A e. C /\ B e. D ) /\ ( B =/= (/) /\ F Fn ( A X. B ) ) ) ->
        ( cur1 ` F ) : A --> { f | f : B --> ran F } ) $=
      ( vx vy wcel wa c0 wne cxp wfn c2nd cv csn cvv cres ccnv ccom crn wf cab
      wral ccur1 cfv wss fss wfun cdm wceq wf1o wi visset 2ndconst ax-mp
      f1ocnv fnfun funco ex syl adantl ad2antlr f1ofun syl5com cima dmco a1i
      cnvcnvres imaeq1i cin df-ima resres rneqd rescnvcnv fndm pm3.27 ineq2d
      reseq2 inxp rneqi xpeq12 snssi df-ss sylib incom inv1 eqtri sylancl
      ad2antrr wfo pm2.27 f1ofo syl6 mp2 forn 3eqtrd eqtrd exp31 com13 impcom
      imp jca df-fn sylibr dffn3 rncoss wb cofunex2g fnex xpexg syl5 adantr
      fo2nd fofun funres funcnvcnv sylanc feq1 elabg mpbird r19.21aiva copab
      pm3.22 pm3.26 cur1vald fopab2g 3syl mpbid ) ACIBDIJZBKLZFABMZNZJZJZFOGPZQ
      ZRMZSZTZUAZBFUBZEPZUCZEUDZIZGAUEZAUUFFUFUGZUCZYPUUGGAYPYQAIZJZUUGBUUCUUBU
      CZBUUBUBZUUBUCZUUNUUCUHUUMUULBUUNUUCUUBUIUULUUBBNZUUOUULUUBUJZUUBUKZBULZJ
      UUPUULUUQUUSRYSUUAUMZUULUUQUNYSRYTUMZUUTYQRIZUVAGUOZYQRRUPUQYSRYTURUQUULU
      UAUJZUUQUUTYOUVDUUQUNZYKUUKYNUVEYLYNFUJZUVEYMFUSUVFUVDUUQFUUAUTVAVBVCVDRY
      SUUAVEVFUQUULUURUUATZFUKZVGZOTTZYSSZUVHVGZBUURUVIULUULFUUAVHVIUVIUVLULUUL
      UVGUVKUVHOYSVJVKVIUULUVLUVKUVHSZUBZUVJYSUVHVLZSZUBZBUVLUVNULUULUVKUVHVMVI
      UULUVMUVPUVMUVPULUULUVJYSUVHVNVIVOUULUVQOUVOSZUBZBUULUVPUVRUVPUVRULUULOUV
      OVPVIVOYPUUKUVSBULZYOYKUUKUVTUNZYNYKUWAUNZYLYNUVHYMULZUWBYMFVQUUKYKUWCUVT
      UUKYKUWCUVTUUKYKJZUWCJZUVSOYSYMVLZSZUBZBUWEUVRUWGUWEUVOUWFULUVRUWGULUWEUV
      HYMYSUWDUWCVRVSUVOUWFOVTVBVOUWEUWHOYRAVLZRBVLZMZSZUBZOYRBMZSZUBZBUWHUWMUL
      UWEUWGUWLUWFUWKULUWGUWLULYRRABWAUWFUWKOVTUQWBVIUUKUWMUWPULYKUWCUUKUWLUWOU
      UKUWKUWNULZUWLUWOULUWIYRULZUWJBULUWQUUKUWIYRUWJBWCUUKYRAUHUWRYQAWDYRAWEWF
      UWJBRVLBRBWGBWHWIWJUWKUWNOVTVBVOWKUWEUWNBUWOWLZUWPBULUWSUWEUVBUVBUWNBUWOU
      MZUNZUWSUVCYQBRUPUVBUXAUWTUWSUVBUWTWMUWNBUWOWNWOWPVIUWNBUWOWQVBWRWSWTXAVB
      VCXBXCWSWRWRXDUUBBXEXFBUUBXGWFFUUAXHWJUULUUBRIZUUGUUMXIFRIZUVGUJZUXBUULFU
      UARXJYPUXCUUKYOYKUXCYNYKUXCUNYLYNYMRIZUXCYKYNUXEUXCYMRFXKVAABCDXLXMVCXBXN
      UULYTUJZUXDUXFUULOUJZUXFRROWLUXGXORROXPUQYSOXQUQVIYTXRVBXSUUEUUMEUUBRBUUC
      UUDUUBXTYAVBYBYCYPYNYLJZYKJUUIUUKHPUUBULJGHYDULUUHUUJXIYPUXHYKYOUXHYKYLYN
      YEVCYKYOYFXDGHABCDFYGGHAUUFUUBUUIYHYIYJ $.
      $( [18-May-2010] $) $( [17-May-2010] $)
  $}

  ${
    $d B f $.  $d F f $.  $d O f $.
    $( The value of a curried function at ` O e. A ` is a mapping. $)
    valcurfn $p |- ( ( ( A e. C /\ B e. D ) /\
               ( B =/= (/) /\ F Fn ( A X. B ) ) /\ O e. A )
        ->
        ( ( cur1 ` F ) ` O ) : B --> ran F ) $=
      ( vf wcel wa c0 wne cxp wfn crn ccur1 cfv wf cv cab wi domrancur1c
      ffvelrn fvex feq1 elab sylib ex syl 3impia ) ACHBDHIZBJKEABLMIZFAHZBENZFE
      OPZPZQZUJUKIABUMGRZQZGSZUNQZULUPTABCDGEUAUTULUPUTULIUOUSHUPAUSFUNUBURUPGU
      OFUNUCBUMUQUOUDUEUFUGUHUI $.
      $( [18-May-2010] $) $( [17-May-2010] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d O x y $.
    valcurfn1.1 $e |- G = ( F o. `' ( 2nd |` ( { O } X. _V ) ) ) $.
    $( The value of a curried function at ` O e. A ` is a partial
       application. $)
    valcurfn1 $p |- ( ( ( A e. C /\ B e. D ) /\
               ( B =/= (/) /\ F Fn ( A X. B ) ) /\ O e. A )
        ->
        ( ( cur1 ` F ) ` O ) = G ) $=
      ( vx vy wcel wa c0 wne cxp wfn w3a ccur1 cfv cv cdm c2nd csn cvv cres
      ccnv ccom wceq copab wfun wrel cur1val wi fnex ex xpexg syl5 adantl
      impcom 3adant3 fnfun 3ad2ant2 fndm relxp releq mpbiri syl syl3anc fveq1d
      sneq xpeq1 reseq2 cnveq coeq2d syl6eqr eqid fvopab4g dmeq eqtr eqcomd
      dmxp eleq2d biimpa 3adant1 simprr pm3.26 jca domrancur1clem syl5eqel
      sylanc eqtrd ) ACKBDKLZBMNZEABOZPZLZGAKZQZGERSZSGITZEUAZUAZKJTEUBWTUCZUDO
      ZUEZUFZUGZUHLIJUIZSZFWRGWSXHEUDKZEUJZXAUKZWSXHUHWRIJUDEULWLWPXJWQWPWLXJWO
      WLXJUMWMWOWNUDKZXJWLWOXMXJWNUDEUNUOABCDUPUQURUSUTWPWLXKWQWOXKWMWNEVAURVBW
      PWLXLWQWOXLWMWOXAWNUHZXLWNEVCZXNXLWNUKABVDXAWNVEVFVGURVBVHVIGXBKZFUDKXIFU
      HWRIJGXGFXBUDXHWTGUHZXGEUBGUCZUDOZUEZUFZUGZFXQXFYAEXQXEXTUHZXFYAUHXQXDXSU
      HZYCXQXCXRUHYDWTGVJXCXRUDVKVGXDXSUBVLVGXEXTVMVGVNHVOXHVPVQWPWQXPWLWPWQXPW
      PAXBGWOWMAXBUHZWOXNWMYEUMZXOXNXBWNUAZUHZYFXAWNVRYHYGAUHZYEWMYHYIYEYHYILXB
      AXBYGAVSVTUOABWAUQVGVGUSWBWCWDWRYBUDFWRWOWLLZYBUDKWLWPYJWQWLWPLWOWLWLWMWO
      WEWLWPWFWGUTABCDEXSWHVGHWIWJWK $.
      $( [18-May-2010] $) $( [17-May-2010] $)

  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d O x y $.
    $( The value of a curried function at ` O e. A ` is a partial
       application. $)
    valcurfn2 $p |- ( ( ( A e. C /\ B e. D ) /\
               ( B =/= (/) /\ F Fn ( A X. B ) ) /\ O e. A )
        ->
        ( ( cur1 ` F ) ` O ) =
        { <. x , y >. | ( x e. B /\ y = ( O F x ) ) } ) $=
      ( wcel wa c0 wne cxp wfn w3a ccur1 cfv c2nd csn cvv cres ccnv ccom cv co
      wceq copab eqid valcurfn1 curry1 simprr 3adant3 3simp3 sylanc eqtrd ) CEI
      DFIJZDKLZGCDMNZJZHCIZOZHGPQQGRHSTMUAUBUCZAUDZDIBUDHVCGUEUFJABUGZCDEFGVBHV
      BUHZUIURUTVBVDUFVAABCDHGVBVEUJUPUSURUTUPUQURUKULUPUSUTUMUNUO $.
      $( [18-May-2010] $) $( [17-May-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d O x y $.  $d P x y $.
    $( The value at ` P e. B ` of the value of a curried function at ` O e. A `
       equals ` ( O F P ) ` . $)
    valvalcurfn $p |- ( ( ( A e. C /\ B e. D ) /\
               ( B =/= (/) /\ F Fn ( A X. B ) ) /\ ( O e. A /\ P e. B ) )
        ->
        ( ( ( cur1 ` F ) ` O ) ` P ) = ( O F P ) ) $=
      ( vx vy wcel wa c0 wne cxp wfn w3a ccur1 cfv cv co wceq copab valcurfn2
      3adant3r fveq1d cvv opreq2 eqid fvopab4g pm3.27 3ad2ant3 oprex sylancl
      eqtrd ) ACJBDJKZBLMFABNOKZGAJZEBJZKZPZEGFQRRZREHSZBJISGVBFTZUAKHIUBZRZGEF
      TZUTEVAVDUOUPUQVAVDUAURHIABCDFGUCUDUEURVFUFJVEVFUAUTHIEVCVFBUFVDVBEGFUGVD
      UHUIUSUOURUPUQURUJUKGEFULUMUN $.
      $( [18-May-2010] $) $( [17-May-2010] $)
  $}


