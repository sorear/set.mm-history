$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Lattice (algebraic definition)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c LatAlg $.

  $( Extend class notation to include the class of lattices. $)
  clatalg $a class LatAlg $.

  ${
    $d j m t x y z $.
    $( Algebraic definition of a lattice. ` j ` is called the join of the
       lattice (and is denoted by ` \/ ` in textbooks) , ` m ` is called the
       meet (and is denoted by ` /\ ` in textbooks). $)
    df-latalg $a |- LatAlg = { <. j , m >. | E. t ( j : ( t X. t ) --> t /\
    m : ( t X. t ) --> t /\
    A. x e. t A. y e. t ( ( x j y ) = ( y j x ) /\ ( x m y ) = ( y m x ) /\
     ( ( x m ( x j y ) ) = x /\ ( x j ( x m y ) ) = x /\
     A. z e. t ( ( x m ( y m z ) ) = ( ( x m y ) m z ) /\
                 ( x j ( y j z ) ) = ( ( x j y ) j z ) ) ) ) ) } $.
  $}

  ${
    $d J j m t x y z $.  $d M j m x y z $.  $d X j m t x y z $.
    islatalg.1 $e |- X = dom dom J $.
    $( The predicate "is a lattice".  (Contributed by FL, 11-Dec-2009.) $)
    islatalg $p |- ( ( J e. A /\ M e. B ) -> ( <. J , M >. e. LatAlg <->
   ( J : ( X X. X ) --> X /\ M : ( X X. X ) --> X /\
    A. x e. X A. y e. X ( ( x J y ) = ( y J x ) /\ ( x M y ) = ( y M x ) /\
     ( ( x M ( x J y ) ) = x /\ ( x J ( x M y ) ) = x /\
     A. z e. X ( ( x M ( y M z ) ) = ( ( x M y ) M z ) /\
                 ( x J ( y J z ) ) = ( ( x J y ) J z ) ) ) ) ) ) ) $=
      ( vt wcel wa cv wf co wceq wral w3a opreq ralbidv vj vm cop clatalg copab
      cxp wex df-latalg a1i eleq2d cdm wb dmxpid dmeq syl5eqr 3ad2ant1 pm4.71ri
      fdm eqcoms exbii dmeqd eqeq2d feq1 eqeq12d opreq2d eqeq1d eqidd opreq123d
      syl id anbi2d 3anbi123d 3anbi13d exbidv eqcomi eqeq2i anbi1d bitrd visset
      anbi12d cvv dmex eleq1 biimpd eleq1i syl6ib mpi 3syl xpeq12 feq23 mpancom
      anidms raleq 3anbi3d raleqbidv ceqsexgv 3bitrd 3anbi23d opelopabg ) FDKGE
      KLZFGUCZUDKXAJMZXBUFZXBUAMZNZXCXBUBMZNZAMZBMZXDOZXIXHXDOZPZXHXIXFOZXIXHXF
      OZPZXHXJXFOZXHPZXHXMXDOZXHPZXHXICMZXFOZXFOZXMXTXFOZPZXHXIXTXDOZXDOZXJXTXD
      OZPZLZCXBQZRZRZBXBQZAXBQZRZJUGZUAUBUEZKHHUFZHFNZYRHGNZXHXIFOZXIXHFOZPZXHX
      IGOZXIXHGOZPZXHUUAGOZXHPZXHUUDFOZXHPZXHXIXTGOZGOZUUDXTGOZPZXHXIXTFOZFOZUU
      AXTFOZPZLZCHQZRZRZBHQZAHQZRZWTUDYQXAUDYQPWTABCJUAUBUHUIUJYPYSYRHXFNZUUCXO
      XHUUAXFOZXHPZXHXMFOZXHPZYDUURLZCHQZRZRZBHQZAHQZRZUVEUAUBFGDEXDFPZYPXBXDUK
      ZUKZPZYOLZJUGZXBHPZXCXBFNZXGUUCXOUVHUVJUVKCXBQZRZRZBXBQZAXBQZRZLZJUGZUVQY
      PUWCULUVRYOUWBJYOUWAXEXGUWAYNXEUVSXCPUWAXCXBXDURUWAXCUVSXCUVSPXBXCUKUVTXB
      UMXCUVSUNUOUSVIUPUQUTUIUVRUWCXBFUKZUKZPZUWKLZJUGUWMUVRUWBUWQJUVRUWAUWPYOU
      WKUVRUVTUWOXBUVRUVSUWNXDFUNZVAVBUVRXEUWEYNUWJXGXCXBXDFVCUVRYMUWIAXBUVRYLU
      WHBXBUVRXLUUCYKUWGXOUVRXJUUAXKUUBXHXIXDFSZXIXHXDFSVDUVRXQUVHXSUVJYJUWFUVR
      XPUVGXHUVRXJUUAXHXFUWSVEVFUVRXRUVIXHXHXMXDFSVFUVRYIUVKCXBUVRYHUURYDUVRYFU
      UPYGUUQUVRXHXHYEUUOXDFUVRVJZUVRXHVGXIXTXDFSVHUVRXJUUAXTXTXDFUWTUWSUVRXTVG
      VHVDVKTVLVMTTVMVTVNUVRUWQUWLJUVRUWPUWDUWKUWPUWDULUVRUWOHXBHUWOIVOZVPUIVQV
      NVRUVRHWAKZUWMUVQULUVRUVSUWNPUVTUWOPZUXBUWRUVSUWNUNUXCUVTWAKZUXBUVSXDUAVS
      WBWBUXCUXDUWOWAKZUXBUXCUXDUXEUVTUWOWAWCWDUWOHWAUXAWEWFWGWHUWKUVQJHWAUWDUW
      EYSXGUVFUWJUVPXCYRPZUWDUWEYSULUWDUXFXBHXBHWIWLZXCXBYRHFWJWKUXFUWDXGUVFULU
      XGXCXBYRHXFWJWKUWDUWIUVOAXBHUWDVJZUWDUWHUVNBXBHUXHUWDUWGUVMUUCXOUWDUWFUVL
      UVHUVJUVKCXBHWMWNWNWOWOVLWPVIWQXFGPZUVFYTUVPUVDYSYRHXFGVCUXIUVOUVCAHUXIUV
      NUVBBHUXIXOUUFUVMUVAUUCUXIXMUUDXNUUEXHXIXFGSZXIXHXFGSVDUXIUVHUUHUVJUUJUVL
      UUTUXIUVGUUGXHXHUUAXFGSVFUXIUVIUUIXHUXIXMUUDXHFUXJVEVFUXIUVKUUSCHUXIYDUUN
      UURUXIYBUULYCUUMUXIXHXHYAUUKXFGUXIVJZUXIXHVGXIXTXFGSVHUXIXMUUDXTXTXFGUXKU
      XJUXIXTVGVHVDVQTVLWRTTWRWSVR $.
      $( [11-Dec-2009] $)
  $}

  ${
    $d J x y z $.  $d M x y z $.  $d X x y z $.
    jop1 $e |- X = dom dom J $.
    ${
      $( Join is a binary internal operation.  (Contributed by FL,
         12-Dec-2009.) $)
      jop $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> J : ( X X. X ) --> X ) $=
        ( vx vy vz wcel cop clatalg cxp wf wa cv co wceq wral w3a syl6bi 3impia
        islatalg simp1 ) CAJZDBJZCDKLJZEEMZECNZUEUFOUGUIUHEDNZGPZHPZCQZULUKCQRU
        KULDQZULUKDQRUKUMDQUKRUKUNCQUKRUKULIPZDQDQUNUODQRUKULUOCQCQUMUOCQROIEST
        THESGESZTUIGHIABCDEFUCUIUJUPUDUAUB $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Meet is a binary internal operation.  (Contributed by FL,
         12-Dec-2009.) $)
      mop $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> M : ( X X. X ) --> X ) $=
        ( vx vy vz wcel cop clatalg cxp wf wa cv co wceq wral w3a syl6bi 3impia
        islatalg simp2 ) CAJZDBJZCDKLJZEEMZEDNZUEUFOUGUHECNZUIGPZHPZCQZULUKCQRU
        KULDQZULUKDQRUKUMDQUKRUKUNCQUKRUKULIPZDQDQUNUODQRUKULUOCQCQUMUOCQROIEST
        THESGESZTUIGHIABCDEFUCUJUIUPUDUAUB $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Closure of join.  (Contributed by FL, 12-Dec-2009.) $)
      cljo $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> ( ( P e. X /\ Q e. X ) -> ( P J Q ) e. X ) ) $=
        ( wcel cop clatalg w3a wa cxp wf co jop adantr simprl simprr ex foprrn
        3jca syl6 ) EAIFBIEFJKILZCGIZDGIZMZGGNGEOZUFUGLZCDEPGIUEUHUJUEUHMUIUFUG
        UEUIUHABEFGHQRUEUFUGSUEUFUGTUCUACDGGGEUBUD $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Closure of meet.  (Contributed by FL, 12-Dec-2009.) $)
      clme $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> ( ( P e. X /\ Q e. X ) -> ( P M Q ) e. X ) ) $=
        ( wcel cop clatalg w3a wa cxp wf co mop adantr simprl simprr ex foprrn
        3jca syl6 ) EAIFBIEFJKILZCGIZDGIZMZGGNGFOZUFUGLZCDFPGIUEUHUJUEUHMUIUFUG
        UEUIUHABEFGHQRUEUFUGSUEUFUGTUCUACDGGGFUBUD $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Absorption law. ` ( x /\ ( x \/ y ) ) = x ` .  (Contributed by FL,
         12-Dec-2009.) $)
      labs1 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> A. x e. X A. y e. X ( x M ( x J y ) ) = x ) $=
        ( vz wcel cop clatalg cv co wceq wral wa wf w3a ralimi islatalg simp31
        cxp 3ad2ant3 syl6bi 3impia ) ECJZFDJZEFKLJZAMZUJBMZENZFNUJOZBGPZAGPZUGU
        HQUIGGUCZGERZUPGFRZULUKUJENOZUJUKFNZUKUJFNOZUMUJUTENUJOZUJUKIMZFNFNUTVC
        FNOUJUKVCENENULVCENOQIGPZSSZBGPZAGPZSUOABICDEFGHUAVGUQUOURVFUNAGVEUMBGU
        SVAUMVBVDUBTTUDUEUF $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y $.  $d M x y $.  $d P x y $.  $d Q y $.  $d X x y $.
      $( Absorption law. ` ( P /\ ( P \/ Q ) ) = P ` .  (Contributed by FL,
         12-Dec-2009.) $)
      labss1 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
           -> ( ( P e. X /\ Q e. X ) -> ( P M ( P J Q ) ) = P ) ) $=
        ( vx vy wcel cop clatalg w3a cv co wceq wral wa labs1 id opreq1 eqeq12d
        opreq12d opreq2 opreq2d eqeq1d rcla42v syl5com ) EAKFBKEFLMKNIOZUJJOZEP
        ZFPZUJQZJGRIGRCGKDGKSCCDEPZFPZCQZIJABEFGHTUNUQCCUKEPZFPZCQIJCDGGUJCQZUM
        USUJCUTUJCULURFUTUAZUJCUKEUBUDVAUCUKDQZUSUPCVBURUOCFUKDCEUEUFUGUHUI $.
        $( [12-Dec-2009] $)
    $}


    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Absorption law. ` ( x \/ ( x /\ y ) ) = x ` .  (Contributed by FL,
         12-Dec-2009.) $)
      labs2 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
           -> A. x e. X A. y e. X ( x J ( x M y ) ) = x ) $=
        ( vz wcel cop clatalg cv co wceq wral wa wf w3a ralimi islatalg simp32
        cxp 3ad2ant3 syl6bi 3impia ) ECJZFDJZEFKLJZAMZUJBMZFNZENUJOZBGPZAGPZUGU
        HQUIGGUCZGERZUPGFRZUJUKENZUKUJENOZULUKUJFNOZUJUSFNUJOZUMUJUKIMZFNFNULVC
        FNOUJUKVCENENUSVCENOQIGPZSSZBGPZAGPZSUOABICDEFGHUAVGUQUOURVFUNAGVEUMBGU
        TVAVBUMVDUBTTUDUEUF $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y $.  $d M x y $.  $d P x y $.  $d Q y $.  $d X x y $.
      $( Absorption law. ` ( P \/ ( P /\ Q ) ) = P ` .  (Contributed by FL,
         12-Dec-2009.) $)
      labss2 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
           -> ( ( P e. X /\ Q e. X ) -> ( P J ( P M Q ) ) = P ) ) $=
        ( vx vy wcel cop clatalg w3a cv co wceq wral wa labs2 id opreq1 eqeq12d
        opreq12d opreq2 opreq2d eqeq1d rcla42v syl5com ) EAKFBKEFLMKNIOZUJJOZFP
        ZEPZUJQZJGRIGRCGKDGKSCCDFPZEPZCQZIJABEFGHTUNUQCCUKFPZEPZCQIJCDGGUJCQZUM
        USUJCUTUJCULUREUTUAZUJCUKFUBUDVAUCUKDQZUSUPCVBURUOCEUKDCFUEUFUGUHUI $.
        $( [12-Dec-2009] $)
    $}

    $( Join is idempotent. ` ( P \/ P ) = P ` .  (Contributed by FL,
       12-Dec-2009.) $)
    jidd $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg ) ->
      ( P e. X -> ( P J P ) = P ) ) $=
      ( wcel cop clatalg w3a co wceq wi wa labss1 imp simpl simprl sylc cljo ex
      jca labss2 opreq2 eqtr eqcoms syl expcom anidms com12 ) CFHZDAHEBHDEIJHKZ
      CCDLZCMZULUMUONUMULULOZUOUMUPOZCUNELZCMZCURDLZCMZUOUMUPUSABCCDEFGPQUQUMUL
      UNFHZOVAUMUPRUQULVBUMULULSUMUPVBABCCDEFGUAQUCABCUNDEFGUDTUSUTUNMVAUONZURC
      CDUEVCUNUTUNUTMVAUOUNUTCUFUBUGUHTUIUJUK $.
      $( [12-Dec-2009] $)

    $( Meet is idempotent. ` ( P /\ P ) = P ` .  (Contributed by FL,
       12-Dec-2009.) $)
    midd $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg ) ->
      ( P e. X -> ( P M P ) = P ) ) $=
      ( wcel cop clatalg w3a co wceq wi wa labss2 imp simpl simprl sylc clme ex
      jca labss1 opreq2 eqtr eqcoms syl expcom anidms com12 ) CFHZDAHEBHDEIJHKZ
      CCELZCMZULUMUONUMULULOZUOUMUPOZCUNDLZCMZCURELZCMZUOUMUPUSABCCDEFGPQUQUMUL
      UNFHZOVAUMUPRUQULVBUMULULSUMUPVBABCCDEFGUAQUCABCUNDEFGUDTUSUTUNMVAUONZURC
      CEUEVCUNUTUNUTMVAUOUNUTCUFUBUGUHTUIUJUK $.
      $( [12-Dec-2009] $)
  $}

