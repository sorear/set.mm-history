$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Lattice (algebraic definition)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c LatAlg $.

  $( Extend class notation to include the class of lattices. $)
  clatalg $a class LatAlg $.

  ${ $d j m t x y z $.
  $( Algebraic definition of a lattice. ` j ` is called the join
     of the lattice (and is denoted by ` \/ ` in textbooks) ,
     ` m ` is called the meet (and is denoted
     by ` /\ ` in textbooks). $)
  df-latalg $a |- LatAlg = { <. j , m >. | E. t ( j : ( t X. t ) --> t /\
    m : ( t X. t ) --> t /\
    A. x e. t A. y e. t ( ( x j y ) = ( y j x ) /\  ( x m y ) = ( y m x ) /\
     ( ( x m ( x j y ) ) = x /\ ( x j ( x m y ) ) = x /\
     A. z e. t ( ( x m ( y m z ) ) = ( ( x m y ) m z ) /\
                 ( x j ( y j z ) ) = ( ( x j y ) j z ) ) ) ) ) } $.
   $}

  ${ $d J j m t x y z $. $d M j m x y z $. $d X j m t x y z $.
    islatalg.1 $e |- X = dom dom J $.
    $( The predicate "is a lattice". $)
    islatalg $p |- ( ( J e. A /\ M e. B ) -> ( <. J , M >. e. LatAlg <->
   ( J : ( X X. X ) --> X /\ M : ( X X. X ) --> X /\
    A. x e. X A. y e. X ( ( x J y ) = ( y J x ) /\  ( x M y ) = ( y M x ) /\
     ( ( x M ( x J y ) ) = x /\ ( x J ( x M y ) ) = x /\
     A. z e. X ( ( x M ( y M z ) ) = ( ( x M y ) M z ) /\
                 ( x J ( y J z ) ) = ( ( x J y ) J z ) ) ) ) ) ) ) $=
      ( vt vj vm wcel wa cop clatalg cv cxp wf co wceq wral w3a wex copab
      df-latalg a1i eleq2d cdm wb fdm dmeq dmxpid syl5eqr eqcoms syl 3ad2ant1
      pm4.71ri exbii dmeqd eqeq2d feq1 opreq eqeq12d opreq2d eqeq1d id eqidd
      opreq123d anbi2d ralbidv 3anbi123d 3anbi13d anbi12d exbidv eqcomi eqeq2i
      anbi1d bitrd cvv visset dmex eleq1 biimpd eleq1i syl6ib mpi 3syl xpeq12
      anidms feq23 mpancom raleq1 3anbi3d raleq12d ceqsexgv 3bitrd 3anbi23d
      opelopabg ) FDMGEMNZFGOZPMXAJQZXBRZXBKQZSZXCXBLQZSZAQZBQZXDTZXIXHXDTZUAZX
      HXIXFTZXIXHXFTZUAZXHXJXFTZXHUAZXHXMXDTZXHUAZXHXICQZXFTZXFTZXMXTXFTZUAZXHX
      IXTXDTZXDTZXJXTXDTZUAZNZCXBUBZUCZUCZBXBUBZAXBUBZUCZJUDZKLUEZMHHRZHFSZYRHG
      SZXHXIFTZXIXHFTZUAZXHXIGTZXIXHGTZUAZXHUUAGTZXHUAZXHUUDFTZXHUAZXHXIXTGTZGT
      ZUUDXTGTZUAZXHXIXTFTZFTZUUAXTFTZUAZNZCHUBZUCZUCZBHUBZAHUBZUCZWTPYQXAPYQUA
      WTABCJKLUFUGUHYPYSYRHXFSZUUCXOXHUUAXFTZXHUAZXHXMFTZXHUAZYDUURNZCHUBZUCZUC
      ZBHUBZAHUBZUCZUVEKLFGDEXDFUAZYPXBXDUIZUIZUAZYONZJUDZXBHUAZXCXBFSZXGUUCXOU
      VHUVJUVKCXBUBZUCZUCZBXBUBZAXBUBZUCZNZJUDZUVQYPUWCUJUVRYOUWBJYOUWAXEXGUWAY
      NXEUVSXCUAUWAXCXBXDUKUWAXCUVSXCUVSUAXCUIUVTXBXCUVSULXBUMUNUOUPUQURUSUGUVR
      UWCXBFUIZUIZUAZUWKNZJUDUWMUVRUWBUWQJUVRUWAUWPYOUWKUVRUVTUWOXBUVRUVSUWNXDF
      ULZUTVAUVRXEUWEYNUWJXGXCXBXDFVBUVRYMUWIAXBUVRYLUWHBXBUVRXLUUCYKUWGXOUVRXJ
      UUAXKUUBXHXIXDFVCZXIXHXDFVCVDUVRXQUVHXSUVJYJUWFUVRXPUVGXHUVRXJUUAXHXFUWSV
      EVFUVRXRUVIXHXHXMXDFVCVFUVRYIUVKCXBUVRYHUURYDUVRYFUUPYGUUQUVRXHXHYEUUOXDF
      UVRVGZUVRXHVHXIXTXDFVCVIUVRXJUUAXTXTXDFUWTUWSUVRXTVHVIVDVJVKVLVMVKVKVMVNV
      OUVRUWQUWLJUVRUWPUWDUWKUWPUWDUJUVRUWOHXBHUWOIVPZVQUGVRVOVSUVRHVTMZUWMUVQU
      JUVRUVSUWNUAUVTUWOUAZUXBUWRUVSUWNULUXCUVTVTMZUXBUVSXDKWAWBWBUXCUXDUWOVTMZ
      UXBUXCUXDUXEUVTUWOVTWCWDUWOHVTUXAWEWFWGWHUWKUVQJHVTUWDUWEYSXGUVFUWJUVPXCY
      RUAZUWDUWEYSUJUWDUXFXBHXBHWIWJZXCXBYRHFWKWLUXFUWDXGUVFUJUXGXCXBYRHXFWKWLU
      WDUWIUVOAXBHUWDVGZUWDUWHUVNBXBHUXHUWDUWGUVMUUCXOUWDUWFUVLUVHUVJUVKCXBHWMW
      NWNWOWOVLWPUPWQXFGUAZUVFYTUVPUVDYSYRHXFGVBUXIUVOUVCAHUXIUVNUVBBHUXIXOUUFU
      VMUVAUUCUXIXMUUDXNUUEXHXIXFGVCZXIXHXFGVCVDUXIUVHUUHUVJUUJUVLUUTUXIUVGUUGX
      HXHUUAXFGVCVFUXIUVIUUIXHUXIXMUUDXHFUXJVEVFUXIUVKUUSCHUXIYDUUNUURUXIYBUULY
      CUUMUXIXHXHYAUUKXFGUXIVGZUXIXHVHXIXTXFGVCVIUXIXMUUDXTXTXFGUXKUXJUXIXTVHVI
      VDVRVKVLWRVKVKWRWSVS $.
      $( [14-Dec-2009] $) $( [11-Dec-2009] $)
  $}

  ${ $d J x y z $. $d M x y z $. $d X x y z $.
    jop1 $e |- X = dom dom J $.
    ${
    $( Join is a binary internal operation. $)
    jop $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> J : ( X X. X ) --> X ) $=
      ( vx vy vz wcel cop clatalg cxp wf wa cv co wceq wral w3a islatalg
      3simp1 syl6bi 3impia ) CAJZDBJZCDKLJZEEMZECNZUEUFOUGUIUHEDNZGPZHPZCQZULUK
      CQRUKULDQZULUKDQRUKUMDQUKRUKUNCQUKRUKULIPZDQDQUNUODQRUKULUOCQCQUMUOCQROIE
      STTHESGESZTUIGHIABCDEFUAUIUJUPUBUCUD $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
    $}

    ${ $d J x y z $. $d M x y z $. $d X x y z $.
    $( Meet is a binary internal operation. $)
    mop $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> M : ( X X. X ) --> X ) $=
      ( vx vy vz wcel cop clatalg cxp wf wa cv co wceq wral w3a islatalg
      3simp2 syl6bi 3impia ) CAJZDBJZCDKLJZEEMZEDNZUEUFOUGUHECNZUIGPZHPZCQZULUK
      CQRUKULDQZULUKDQRUKUMDQUKRUKUNCQUKRUKULIPZDQDQUNUODQRUKULUOCQCQUMUOCQROIE
      STTHESGESZTUIGHIABCDEFUAUJUIUPUBUCUD $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
    $}

    ${ $d J x y z $. $d M x y z $. $d X x y z $.
    $( Closure of join. $)
    cljo $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> ( ( P e. X /\ Q e. X ) -> ( P J Q ) e. X ) ) $=
      ( wcel cop clatalg w3a wa cxp wf co jop adantr simprl simprr 3jca ex
      foprrn syl6 ) EAIFBIEFJKILZCGIZDGIZMZGGNGEOZUFUGLZCDEPGIUEUHUJUEUHMUIUFUG
      UEUIUHABEFGHQRUEUFUGSUEUFUGTUAUBCDGGGEUCUD $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
    $}

    ${ $d J x y z $. $d M x y z $. $d X x y z $.
    $( Closure of meet. $)
    clme $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> ( ( P e. X /\ Q e. X ) -> ( P M Q ) e. X ) ) $=
      ( wcel cop clatalg w3a wa cxp wf co mop adantr simprl simprr 3jca ex
      foprrn syl6 ) EAIFBIEFJKILZCGIZDGIZMZGGNGFOZUFUGLZCDFPGIUEUHUJUEUHMUIUFUG
      UEUIUHABEFGHQRUEUFUGSUEUFUGTUAUBCDGGGFUCUD $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
    $}

    ${ $d J x y z $. $d M x y z $. $d X x y z $.
      $( Absorption law. ` ( x /\ ( x \/ y ) ) = x ` $)
      labs1 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> A. x e. X A. y e. X ( x M ( x J y ) ) = x ) $=
        ( vz wcel cop clatalg cv co wceq wral wa cxp wf w3a islatalg 3simp1
        3ad2ant3 r19.20si syl6bi 3impia ) ECJZFDJZEFKLJZAMZUJBMZENZFNUJOZBGPZAG
        PZUGUHQUIGGRZGESZUPGFSZULUKUJENOZUJUKFNZUKUJFNOZUMUJUTENUJOZUJUKIMZFNFN
        UTVCFNOUJUKVCENENULVCENOQIGPZTZTZBGPZAGPZTUOABICDEFGHUAVHUQUOURVGUNAGVF
        UMBGVEUSUMVAUMVBVDUBUCUDUDUCUEUF $.
        $( [14-Dec-2009] $) $( [12-Dec-2009] $)
    $}

    ${ $d J x y $. $d M x y $. $d P x y $. $d Q y $. $d X x y $.
    $( Absorption law.  $)
    labss1 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> ( ( P e. X /\ Q e. X ) -> ( P M ( P J Q ) ) = P ) ) $=
      ( vx vy wcel wa cv co wceq wral cop clatalg w3a id opreq1 opreq12d
      eqeq12d opreq2 opreq2d eqeq1d rcla42v labs1 syl5com ) CGKDGKLIMZUJJMZENZF
      NZUJOZJGPIGPCCDENZFNZCOZEAKFBKEFQRKSUNUQCCUKENZFNZCOIJCDGGUJCOZUMUSUJCUTU
      JCULURFUTTZUJCUKEUAUBVAUCUKDOZUSUPCVBURUOCFUKDCEUDUEUFUGIJABEFGHUHUI $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
    $}


    ${ $d J x y z $. $d M x y z $. $d X x y z $.
    $( Absorption law. ` ( x \/ ( x /\ y ) ) = x ` $)
    labs2 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> A. x e. X A. y e. X ( x J ( x M y ) ) = x ) $=
      ( vz wcel cop clatalg cv co wceq wral wa cxp wf w3a islatalg 3simp2
      3ad2ant3 r19.20si syl6bi 3impia ) ECJZFDJZEFKLJZAMZUJBMZFNZENUJOZBGPZAGPZ
      UGUHQUIGGRZGESZUPGFSZUJUKENZUKUJENOZULUKUJFNOZUJUSFNUJOZUMUJUKIMZFNFNULVC
      FNOUJUKVCENENUSVCENOQIGPZTZTZBGPZAGPZTUOABICDEFGHUAVHUQUOURVGUNAGVFUMBGVE
      UTUMVAVBUMVDUBUCUDUDUCUEUF $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
    $}

    ${ $d J x y $. $d M x y $. $d P x y $. $d Q y $. $d X x y $.
    $( Absorption law. $)
    labss2 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> ( ( P e. X /\ Q e. X ) -> ( P J ( P M Q ) ) = P ) ) $=
      ( vx vy wcel wa cv co wceq wral cop clatalg w3a id opreq1 opreq12d
      eqeq12d opreq2 opreq2d eqeq1d rcla42v labs2 syl5com ) CGKDGKLIMZUJJMZFNZE
      NZUJOZJGPIGPCCDFNZENZCOZEAKFBKEFQRKSUNUQCCUKFNZENZCOIJCDGGUJCOZUMUSUJCUTU
      JCULUREUTTZUJCUKFUAUBVAUCUKDOZUSUPCVBURUOCEUKDCFUDUEUFUGIJABEFGHUHUI $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
    $}

    $( Join is idempotent. ` ( P \/ P ) = P ` $)
    jidd $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg ) ->
      ( P e. X -> ( P J P ) = P ) ) $=
      ( wcel cop clatalg w3a co wceq wi wa opreq2 eqtr ex eqcoms syl labss1
      imp labss2 pm3.26 simprl cljo jca sylc expcom anidms com12 ) CFHZDAHEBHDE
      IJHKZCCDLZCMZULUMUONUMULULOZUOCUNELZCMZCUQDLZCMZUOUMUPOZURUSUNMUTUONZUQCC
      DPVBUNUSUNUSMUTUOUNUSCQRSTUMUPURABCCDEFGUAUBUMULUNFHZOUTVAABCUNDEFGUCUMUP
      UDVAULVCUMULULUEUMUPVCABCCDEFGUFUBUGUHUHUIUJUK $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)

    $( Meet is idempotent. ` ( P /\ P ) = P ` $)
    midd $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg ) ->
      ( P e. X -> ( P M P ) = P ) ) $=
      ( wcel cop clatalg w3a co wceq wi wa opreq2 eqtr ex eqcoms syl labss2
      imp labss1 pm3.26 simprl clme jca sylc expcom anidms com12 ) CFHZDAHEBHDE
      IJHKZCCELZCMZULUMUONUMULULOZUOCUNDLZCMZCUQELZCMZUOUMUPOZURUSUNMUTUONZUQCC
      EPVBUNUSUNUSMUTUOUNUSCQRSTUMUPURABCCDEFGUAUBUMULUNFHZOUTVAABCUNDEFGUCUMUP
      UDVAULVCUMULULUEUMUPVCABCCDEFGUFUBUGUHUHUIUJUK $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
  $}

