$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Real vector spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c RVec $.

  $( Extend class notation with the class of all real vector spaces. $)
  cvr $a class RVec $.

  ${
    $d g s x y z $.
    $( Define the class of all real vector spaces.  The definition of a
       ` RVec ` and a ` CVec ` don't much differ.  There may be a way to get
       both in only one definition.  A ` RVec ` seems mandatory if one wants to
       do classical cartesian geometry.  We can't use a ` CVec ` instead.
       Changing the field changes important properties such as the
       dimension. $)
    df-vr $a |- RVec = { <. g , s >. | ( g e. Abel /\
     s : ( RR X. ran g ) --> ran g /\ A. x e. ran g ( ( 1 s x ) = x /\
       A. y e. RR ( A. z e. ran g ( y s ( x g z ) ) = ( ( y s x ) g ( y s z ) )
         /\ A. z e. RR ( ( ( y + z ) s x ) = ( ( y s x ) g ( z s x ) ) /\
           ( ( y x. z ) s x ) = ( y s ( z s x ) ) ) ) ) ) } $.

    $( The class of all real vector spaces is a relation. $)
    vrrel $p |- Rel RVec $=
      ( vg vs vx vy vz cvr wrel cv cabl wcel cr crn cxp wf c1 co wceq wral
      caddc cmul wa w3a copab relopab df-vr releqi mpbir ) FGAHZIJKUHLZMUIBHZNO
      CHZUJPUKQDHZUKEHZUHPUJPULUKUJPZULUMUJPUHPQEUIRULUMSPUKUJPUNUMUKUJPZUHPQUL
      UMTPUKUJPULUOUJPQUAEKRUADKRUACUIRUBZABUCZGUPABUDFUQCDEABUEUFUG $.
      $( [17-Nov-2008] $) $( [16-Nov-2008] $)
  $}

  ${
    $d G g s x y z $.  $d S g s x y z $.  $d W g s $.  $d X g s x z $.
    vri.1 $e |- G = ( 1st ` W ) $.
    vri.2 $e |- S = ( 2nd ` W ) $.
    vri.3 $e |- X = ran G $.
    $( The properties of a real vector space, which is an abelian group (i.e.
       the vectors, with the operation of vector addition) accompanied by a
       scalar multiplication operation on the field of real numbers.  The
       variable ` W ` was chosen because ` _V ` is already used for the
       universal class. $)
    vri $p |- ( W e. RVec ->
              ( G e. Abel /\
         S : ( RR X. X ) --> X /\ A. x e. X ( ( 1 S x ) = x /\
       A. y e. RR ( A. z e. X ( y S ( x G z ) ) = ( ( y S x ) G ( y S z ) )
         /\ A. z e. RR ( ( ( y + z ) S x ) = ( ( y S x ) G ( z S x ) ) /\
           ( ( y x. z ) S x ) = ( y S ( z S x ) ) ) ) ) ) ) $=
      ( vg vs cvr wcel cv cabl cr crn cxp wf c1 co wceq wral caddc cmul wa w3a
      copab df-vr eleq2i c1st cfv wb eqeq2i eleq1 rneq syl6eqr eqidd xpeq2 id
      3jca feq123 3syl opreq opreq2d eqeq12d raleqbidv eqeq2d anbi1d ralbidv
      anbi12d anbi2d 3anbi123d sylbir c2nd feq1 eqeq1d opreq12d eqtrd 3anbi23d
      elopabi sylbi ) FMNFKOZPNZQWDRZSZWFLOZTZUAAOZWHUBZWJUCZBOZWJCOZWDUBZWHUBZ
      WMWJWHUBZWMWNWHUBZWDUBZUCZCWFUDZWMWNUEUBZWJWHUBZWQWNWJWHUBZWDUBZUCZWMWNUF
      UBZWJWHUBZWMXDWHUBZUCZUGZCQUDZUGZBQUDZUGZAWFUDZUHZKLUIZNEPNZQGSZGDTZUAWJD
      UBZWJUCZWMWJWNEUBZDUBZWMWJDUBZWMWNDUBZEUBZUCZCGUDZXBWJDUBZYFWNWJDUBZEUBZU
      CZXGWJDUBZWMYLDUBZUCZUGZCQUDZUGZBQUDZUGZAGUDZUHZMXRFABCKLUJUKXQXSXTGWHTZW
      LWMYDWHUBZWQWREUBZUCZCGUDZXCWQXDEUBZUCZXJUGZCQUDZUGZBQUDZUGZAGUDZUHZUUDKL
      FWDFULUMZUCWDEUCZXQUURUNEUUSWDHUOUUTWEXSWIUUEXPUUQWDEPUPUUTWFGUCZWHWHUCZW
      GXTUCZUVAUHWIUUEUNUUTWFERGWDEUQJURZUVAUVBUVCUVAUVAWHUSWFGQUTUVAVAVBWGWFXT
      GWHWHVCVDUUTXOUUPAWFGUVDUUTXNUUOWLUUTXMUUNBQUUTXAUUIXLUUMUUTWTUUHCWFGUVDU
      UTWPUUFWSUUGUUTWOYDWMWHWJWNWDEVEVFWQWRWDEVEVGVHUUTXKUULCQUUTXFUUKXJUUTXEU
      UJXCWQXDWDEVEVIVJVKVLVKVMVHVNVOWHFVPUMZUCWHDUCZUURUUDUNDUVEWHIUOUVFUUEYAU
      UQUUCXSXTGWHDVQUVFUUPUUBAGUVFWLYCUUOUUAUVFWKYBWJUAWJWHDVEVRUVFUUNYTBQUVFU
      UIYJUUMYSUVFUUHYICGUVFUUFYEUUGYHWMYDWHDVEUVFWQYFWRYGEWMWJWHDVEZWMWNWHDVEV
      SVGVKUVFUULYRCQUVFUUKYNXJYQUVFXCYKUUJYMXBWJWHDVEUVFWQYFXDYLEUVGWNWJWHDVEZ
      VSVGUVFXHYOXIYPXGWJWHDVEUVFXIWMYLWHUBYPUVFXDYLWMWHUVHVFWMYLWHDVEVTVGVLVKV
      LVKVLVKWAVOWBWC $.
      $( [17-Nov-2008] $) $( [16-Nov-2008] $)
  $}

