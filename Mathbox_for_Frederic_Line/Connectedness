$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Connectedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The closed sets of the topology ` { (/) } ` .  (Contributed by FL,
     5-Jan-2009.) $)
  clsingemp $p |- ( Clsd ` { (/) } ) = { (/) } $=
    ( c0 cpw csn wceq ccld cfv pw0 0ex dtopcl eqcomi fveq2 3eqtr4a eqcoms ax-mp
    id ) ABZACZDQEFZQDZGSQPQPDZPEFZPRQPUAAHIJQPEKTOLMN $.
    $( [5-Jan-2009] $)

  ${
    $d A a x $.
    $( The closed sets of an indiscrete topology.  (Contributed by FL,
       5-Jan-2009; hypothesis eliminated by Mario Carneiro, 11-Nov-2013.) $)
    clindistop $p |- ( Clsd ` { (/) , A } ) = { (/) , A } $=
      ( va vx cvv wcel c0 cpr ccld cfv wceq cv fveq2d cun 0ex visset wa cab wss
      cdif ax-mp preq2 eqeq12d ctop indistop cpw crab cuni unipr uncom 3eqtr2ri
      un0 cldval elpw anbi1i difexg elpr ssdif0 eqss biimpri olcd expcom sylbir
      wo wi cin disj3 incom eqtr reldisj difid sseq2i sylbi orcd syl6bi syl5com
      ss0 eqcoms jaoi impcom 0ss dif0 prid2 eqeltri pm3.2i sseq1 difeq2 anbi12d
      ex eleq1d mpbiri ssid prid1 impbii bitri abbii df-rab dfpr2 syl6eq vtoclg
      3eqtr4i wn csn clsingemp prprc2 3eqtr4a pm2.61i ) ADEZFAGZHIZXHJZFBKZGZHI
      ZXLJZXJBADXKAJZXMXIXLXHXOXLXHHXKAFUAZLXPUBXLUCEZXNXKUDXQXMXKCKZSZXLEZCXKU
      EZUFZXLCXLXKXLUGFXKMXKFMXKFXKNBOZUHXKFUIXKUKUJULXRYAEZXTPZCQXRFJZXRXKJZVC
      ZCQYBXLYEYHCYEXRXKRZXTPZYHYDYIXTXRXKCOUMUNYJYHXTYIYHXTXSFJZXSXKJZVCYIYHVD
      ZXSFXKXKDEXSDEYCXKXRDUOTUPYKYMYLYKXKXRRZYMXKXRUQYIYNYHYIYNPZYGYFYGYOXRXKU
      RUSUTVAVBYMXKXSXKXSJXKXRVEZFJZYMXKXRVFYPXRXKVEZJYQYMVDZXKXRVGYSYRYPYRYPJZ
      YQYMYTYQPYRFJZYIYHYRYPFVHYIUUAXRXKXKSZRZYHXRXKXKVIUUCYFYGUUCXRFRYFUUBFXRX
      KVJZVKXRVPVLVMVNVOWHVQTVBVQVRVLVSYFYJYGYFYJFXKRZXKFSZXLEZPUUEUUGXKVTUUFXK
      XLXKWAFXKYCWBWCWDYFYIUUEXTUUGXRFXKWEYFXSUUFXLXRFXKWFWIWGWJYGYJXKXKRZUUBXL
      EZPUUHUUIXKWKUUBFXLUUDFXKNWLWCWDYGYIUUHXTUUIXRXKXKWEYGXSUUBXLXRXKXKWFWIWG
      WJVRWMWNWOXTCYAWPCFXKWQWTWRTWSXGXAZFXBZHIUUKXIXHXCUUJXHUUKHFAXDZLUULXEXF
      $.
      $( [11-Nov-2013] $) $( [5-Jan-2009] $)

    $( The indiscrete topology is connected.  (Contributed by FL,
       5-Jan-2009.) $)
    intopcon $p |- { (/) , A } e. Con $=
      ( va cvv wcel c0 cpr ccon wceq preq2 eleq1d ctop ccld cfv cin cuni iscon2
      cv indistop cun 0ex clindistop ineq2i inidm visset unipr uncom un0 preq2i
      3eqtr2ri 3eqtri mpbir2an vtoclg wn csn dfsn2 syl6eqr vtocl prprc2 pm2.61i
      mpbiri ) ACDZEAFZGDZEBQZFZGDZVCBACVDAHVEVBGVDAEIJVFVEKDVEVELMZNZEVEOZFZHV
      EPVDRVHVEVENVEVJVGVEVEVDUAUBVEUCVDVIEVIEVDSVDESVDEVDTBUDUEVDEUFVDUGUIUHUJ
      UKZULVAUMZVCEUNZGDZVFVNBETVDEHZVEVMGVOVEEEFVMVDEEIEUOUPJVKUQVLVBVMGEAURJU
      TUS $.
      $( [11-Nov-2013] $) $( [5-Jan-2009] $)
  $}

  $( The singleton of the empty set is a connected topology.  (Contributed by
     FL, 22-Dec-2008.) $)
  singempcon $p |- { (/) } e. Con $=
    ( c0 csn cpr ccon dfsn2 intopcon eqeltri ) ABAACDAEAFG $.
    $( [11-Nov-2013] $) $( [22-Dec-2008] $)

