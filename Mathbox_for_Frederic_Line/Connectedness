$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Connectedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Con $.
  $( Extend class notation with the the class of all connected topologies. $)
  ccon $a class Con $.

  $( Topologies are connected when only ` (/) ` and ` U. j ` are
     both open and closed. $)
  df-con $a |- Con = { j e. Top | ( j i^i ( Clsd ` j ) ) = { (/) , U. j } } $.

  ${ $d J j $.
    $( The predicate ` J ` is a connected topology . $)
    iscon $p |- ( J e. Top -> ( J e. Con <-> ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj ccon wcel ctop ccld cfv cin c0 cuni cpr wceq cv id fveq2 ineq12d
      unieq preq2 syl eqeq12d df-con elrab2 baib ) ACDAEDAAFGZHZIAJZKZLZBMZUIFG
      ZHZIUIJZKZLUHBAECUIALZUKUEUMUGUNUIAUJUDUNNUIAFOPUNULUFLUMUGLUIAQULUFIRSTB
      UAUBUC $.
      $( [17-Nov-2008] $) $( [16-Nov-2008] $)

    $( The predicate ` J ` is a connected topology . $)
    iscon2 $p |- ( J e. Con <-> ( J e. Top /\ ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj cv ccld cfv cin c0 cuni cpr wceq ctop ccon id fveq2 ineq12d unieq
      preq2 syl eqeq12d df-con elrab2 ) BCZUBDEZFZGUBHZIZJAADEZFZGAHZIZJBAKLUBA
      JZUDUHUFUJUKUBAUCUGUKMUBADNOUKUEUIJUFUJJUBAPUEUIGQRSBTUA $.
      $( [17-Nov-2008] $) $( [16-Nov-2008] $)

  $}

  ${ $d J x y $. $d X y $.
    usinuniop.1 $e |- X = U. J $.
    $( If a topology is connected, its underlying set can't be partitioned
       into two non empty non-overlapping open sets. $)
    usinuniop $p |- ( J e. Con -> A. x e. J A. y e. J
       ( ( x =/= (/) /\ y =/= (/) /\ ( x i^i y ) = (/) )
         -> X =/= ( x u. y ) ) ) $=
      ( ccon wcel cv c0 wne cin wceq w3a cun wi wa cdif wn ctop ccld cfv cuni
      cpr wo opncld eleq1 elin eleq2 biimpd visset elpr olc a1i com13 eqcomi
      eqeq2i eqeq2 wss eltopss wal dfss2 cab df-dif eqtr wb abeq2 pm4.71 albii
      19.26 imim1 pm2.01 syl6 impcom 19.20i eq0 sylibr sylbir ex sylbi syl
      eqcoms mpi com12 imp orcd syl6bi jaoi syl6com com24 expcom pm2.43a com3l
      mpancom com14 imp41 nne orbi12i ianor con2d iscon2 sylanb 3adant3 twpar2
      sseq2i eqcom syl5bb 3ad2ant3 elssuni syl5com ad2antrl necon3abid mpbird
      exp31 r19.21aivv ) CFGZAHZIJZBHZIJZXPXRKILZMZDXPXRNZJZOABCCXOXPCGZXRCGZPZ
      YAYCXOYFPZYAPZYCDXPQZXRLZRZYAYGYKXQXSYGYKOXTYGXQXSPZYKCSGZCCTUAZKZICUBZUC
      ZLZPZYFYLYKOXOYSYFPZYJYLYTYJYLRZYTYJPZXQRZXSRZUDZUUAUUBXPILZXRILZUDZUUEYM
      YRYFYJUUHYFYRYMYJUUHOZYEYDYRYMUUIOOYMYDYRYEUUIYMYDYRYEUUIOOZYIYNGZYMYDPZU
      UJXPCDEUEUUKUULPYJYEYRUUHUUKUULYJYEYRUUHOZOZOYJUUKUULUUNUUKYJUULUUNOZYJUU
      KXRYNGZYJUUOOYIXRYNUFUUPYEUULYJUUMYEUUPUULYJUUMOOZYEUUPPXRYOGZUUQXRCYNUGU
      URYRYJUULUUHYRUURXRYQGZYJUULUUHOZOZYRUURUUSYOYQXRUHUIUUSUUGXRYPLZUDUVAXRI
      YPBUJUKUUGUVAUVBUULYJUUGUUHYJUUGUUHOZOUULUVCYJUUGUUFULUMUMUNUVBXRDLZUVAYP
      DXRDYPEUOZUPUVDYJYIDLZUUTXRDYIUQUULUVFUUHUULXPDURZUVFUUHOXPCDEUSUVGUVFUUH
      UVGUVFPUUFUUGUVGUVFUUFUVGXRXPGZXRDGZOZBUTZUVFUUFOBXPDVAUVFUVKUUFUVFYIUVIU
      VHRZPZBVBZLZUVKUUFOZBDXPVCUVOUVPODYIDYILZUVOUVPUVQUVOPDUVNLZUVPDYIUVNVDUV
      RUVIUVMVEZBUTZUVPUVMBDVFUVTUVIUVLOZBUTZUVPUWAUVSBUVIUVLVGVHUWBUVKUUFUWBUV
      KPUWAUVJPZBUTZUUFUWAUVJBVIUWDUVLBUTUUFUWCUVLBUVJUWAUVLUVJUWAUVHUVLOUVLUVH
      UVIUVLVJUVHVKVLVMVNBXPVOVPVQVRVQVSVTVRWAWBWCVSWDWEVRVTWCWFVSWGVSWHWIVQWJW
      IWFWKWLWDWIWMVRWNVMUNWOUUCUUFUUDUUGXPIWPXRIWPWQVPXQXSWRVPVRWSCWTXAWCXBVMY
      HYJDYBYGYADYBLZYJVEZYDYAUWFOXOYEYAXPYPURZUWFYDXTXQUWGUWFOXSUWGXTUWFUWGXTP
      YBDLZYJUWEUVGXTUWHYJVEUWGXPXRDXCYPDXPUVEXDXADYBXEXFWJXGXPCXHXIXJWDXKXLXMX
      N $.
      $( [17-Nov-2008] $) $( [16-Nov-2008] $)
  $}

  $( A connected topology is a topology. $)
  contop $p |- Con (_ Top $=
    ( vx ccon ctop cv wcel ccld cfv cin c0 cuni cpr wceq iscon2 pm3.26bi ssriv
    ) ABCADZBEPCEPPFGHIPJKLPMNO $.
    $( [22-Dec-2008] $) $( [22-Dec-2008] $)

  $( The closed sets of the topology ` { (/) } ` . $)
  clsingemp $p |- ( Clsd ` { (/) } ) = { (/) } $=
    ( c0 cpw csn wceq ccld cfv pw0 0ex dtopcl eqcomi fveq2 id 3eqtr4a eqcoms
    ax-mp ) ABZACZDQEFZQDZGSQPQPDZPEFZPRQPUAAHIJQPEKTLMNO $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)


  ${ $d A x $.
    clindistop.1 $e |- A e. B $.
    $( The closed sets of an indiscrete topology. $)
    clindistop $p |- ( Clsd ` { (/) , A } ) = { (/) , A } $=
      ( vx c0 cpr ctop wcel ccld cfv wceq indistop cv wss cdif wa cab cuni cun
      0ex elisseti unipr uncom un0 3eqtr2ri cldval wo wi cvv difexg ax-mp elpr
      ssdif0 eqss biimpri olcd expcom sylbir cin disj3 incom reldisj difid
      sseq2i ss0 sylbi orcd syl6bi eqtr syl5com ex eqcoms jaoi impcom 0ss dif0
      prid2 eqeltri pm3.2i sseq1 difeq2 eleq1d anbi12d mpbiri ssid prid1
      impbii abbii dfpr2 eqtr4i syl6eq ) EAFZGHZWLIJZWLKALWMWNDMZANZAWOOZWLHZPZ
      DQZWLDWLAWLREASAESAEATABCUAZUBAEUCAUDUEUFWTWOEKZWOAKZUGZDQWLWSXDDWSXDWRWP
      XDWRWQEKZWQAKZUGWPXDUHZWQEAABHWQUIHCAWOBUJUKULXEXGXFXEAWONZXGAWOUMWPXHXDW
      PXHPZXCXBXCXIWOAUNUOUPUQURXGAWQAWQKAWOUSZEKZXGAWOUTXJWOAUSZKXKXGUHZAWOVAX
      MXLXJXLXJKZXKXGWPXLEKZXDXNXKPWPXOWOAAOZNZXDWOAAVBXQXBXCXQWOENXBXPEWOAVCZV
      DWOVEVFVGVHXLXJEVIVJVKVLUKURVLVMVFVNXBWSXCXBWSEANZAEOZWLHZPXSYAAVOXTAWLAV
      PEAXAVQVRVSXBWPXSWRYAWOEAVTXBWQXTWLWOEAWAWBWCWDXCWSAANZXPWLHZPYBYCAWEXPEW
      LXREATWFVRVSXCWPYBWRYCWOAAVTXCWQXPWLWOAAWAWBWCWDVMWGWHDEAWIWJWKUK $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  $( The closed sets of an indiscrete topology. $)
  clindistop2 $p |- ( A e. B -> ( Clsd ` { (/) , A } ) = { (/) , A } ) $=
    ( wcel cvv c0 cpr ccld cfv wceq elisset cif preq2 fveq2d eqeq12d 0ex
    elimel clindistop dedth syl ) ABCADCZEAFZGHZUAIZABJTUCETAEKZFZGHZUEIAEAUDIZ
    UBUFUAUEUGUAUEGAUDELZMUHNUDDAEDOPQRS $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)

  ${
    intopcon.1 $e |- A e. B $.
    $( The indiscrete topology is connected. $)
    intopcon $p |- { (/) , A } e. Con $=
      ( c0 cpr ccon wcel ctop ccld cfv cin cuni wceq iscon2 indistop
      clindistop ineq2i inidm cun 0ex elisseti unipr uncom un0 3eqtr2ri preq2
      ax-mp 3eqtri mpbir2an ) DAEZFGUJHGUJUJIJZKZDUJLZEZMUJNAOULUJUJKUJUNUKUJUJ
      ABCPQUJRAUMMUJUNMUMDASADSADATABCUAUBADUCAUDUEAUMDUFUGUHUI $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  $( The singleton of the empty set is a connected topology. $)
  singempcon $p |- { (/) } e. Con $=
    ( vx c0 csn ccon wcel ccld cfv cin cuni cpr wceq cv wss cdif wa cab ss0
    adantr eqimss difeq2 difid 0ex snid eqeltri syl6eqel jca impbii abbii ctop
    sn0top unisn eqcomi cldval ax-mp df-sn 3eqtr4i ineq2i inidm dfsn2 preq2
    eqtri 3eqtri wb iscon mpbir ) BCZDEZVFVFFGZHZBVFIZJZKZVIVFVFHVFVKVHVFVFALZB
    MZBVMNZVFEZOZAPZVMBKZAPVHVFVQVSAVQVSVNVSVPVMQRVSVNVPVMBSVSVOBBNZVFVMBBTVTBV
    FBUABUBUCUDUEUFUGUHVFUIEZVHVRKUJAVFBVJBBUBUKULZUMUNABUOUPUQVFURVFBBJZVKBUSB
    VJKWCVKKWBBVJBUTUNVAVBWAVGVLVCUJVFVDUNVE $.
    $( [22-Dec-2008] $) $( [22-Dec-2008] $)

  $( ` (/) ` is a connected part of any connected topology. $)
  empcon $p |- ( J e. Con -> ( subSp ` <. (/) , J >. ) e. Con ) $=
    ( ccon wcel c0 cop csubsp cfv csn ctop wceq contop sseli subspemp2 syl
    singempcon syl6eqel ) ABCZDAEFGZDHZBQAICRSJBIAKLAMNOP $.
    $( [22-Dec-2008] $) $( [22-Dec-2008] $)

  ${
    topsinind.1 $e |- A e. B $.
    $( The only topology on a singleton is the indiscrete topology. $)
    topsinind $p |- ( J e. Top -> ( U. J = { A } <->
      J = { (/) , { A } } ) ) $=
      ( ctop wcel c0 cuni cpr wss cpw wa csn wceq wb topindis wi pweq sseq2d
      preq2 sseq1d anbi12d pwsn sseq2 anbi1d eqss biimpri syl6bi ax-mp exp3a
      com13 imp unieq cun 0ex snex unipr uncom un0 3eqtr2i syl6eq impbid1 syl
      ) CEFGCHZIZCJZCVDKZJZLZVDAMZNZCGVJIZNZOCPVIVKVMVFVHVKVMQVKVHVFVMVKVHVFVMV
      KVHVFLCVJKZJZVLCJZLZVMVKVHVOVFVPVKVGVNCVDVJRSVKVEVLCVDVJGTUAUBVNVLNZVQVMQ
      AUCVRVQCVLJZVPLZVMVRVOVSVPVNVLCUDUEVMVTCVLUFUGUHUIUHUJUKULVMVDVLHZVJCVLUM
      WAGVJUNVJGUNVJGVJUOAUPUQVJGURVJUSUTVAVBVC $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${
    singcon.1 $e |- X = U. J $.
    $( A singleton is a connected part of any topology. $)
    singcon $p |- ( ( J e. Top /\ A e. X ) ->
      ( subSp ` <. { A } , J >. ) e. Con ) $=
      ( ctop wcel wa csn cop csubsp cfv ccon ccld cin c0 cuni cpr wceq
      subtopsin2 wss stoig2 eqcomd snssg eleq2i syl5bb ibi sylan2 preq2 syl
      eqtrd fveq2d cvv snex clindistop a1i 3eqtrd ineq12d inidm syl6eq wb
      stoig3 iscon mpbird ) BEFZACFZGZAHZBIJKZLFZVHVHMKZNZOVHPZQZRZVFVKVMVMNVMV
      FVHVMVJVMVFVHOVGQZVMABCDSZVFVGVLRZVOVMRZVDVGBPZTZVQVEVDVTGVLVGVGBUAZUBVEV
      TVEAVSFVTVEAVSCUCCVSADUDUEUFZUGVGVLOUHZUIUJVFVJVOMKZVOVMVFVHVOMVPUKWDVORV
      FVGULAUMUNUOVFVQVRVFVLVGVDVTVLVGRVEWAWBUGUBWCUIUPUQVMURUSVFVHEFZVIVNUTVDV
      TWEVEVGBVAWBUGVHVBUIVC $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

