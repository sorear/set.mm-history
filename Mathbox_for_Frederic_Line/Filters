$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Filters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A u x y w $.  $d B a b u v x y z $.  $d X a b x y z $.
    fgsb.1 $e |- B = { x | E. y
                  ( y C_ A /\ y e. Fin /\ x = |^| y ) } $.
    $( Filter generated by a subbasis ` A ` .  Bourbaki TG I.37 paragraph above
       prop. 1.  The theorem has been slightly modified because the definitions
       of the empty set are different in Bourbaki and Metamath. $)
    fgsb $p |- ( ( A C_ ~P X /\ X e. _V /\ A =/= (/) ) -> ( -. (/) e. B ->
      { x e. ~P X | E. y e. B y C_ x } e. Fil ) ) $=
      ( va vb vz vu vw vv cpw wss cvv wcel c0 wne w3a wn cv wrex crab cfil wa
      cuni wi wal cin wral wceq sseq2 rexbidv elrab ss0 eleq1d biimpcd
      r19.23aiv adantl sylbi con3i ump 3ad2ant2 cfn cint wex cab fine neeq1i
      sylibr 3ad2ant3 ssid ax-17 eleq1 sseq1 imbi12d eleq2i fiiu chvar uniss
      3ad2ant1 sylan9ssr unipw syl6sseq visset elpw simpr rcla4ev sylancl
      cbvrexv syl5bb sylanbrc ssuni sylancr r19.21aiva r19.2z syl11anc jca
      adantr hbrab1 hbuni hbpw hbss hbrex hbre1 hbrab hbeq rexbid elrabf
      unissb elpwi mprgbir sspwb mpbi sseli sylbir r19.41v sstr reximi adantll
      3adant2 syl3an1b gen2 a1i simpl sseq2d rexbidva inpws1 biimpi anim12i
      infi1 eqcomi 3syl ss2in rcla4e syl2an an4s expcom r19.23aiva com12 imp
      syl2anb rgen2 3jca wb pwexg rabexg syl eqid isfil mpbird ex ) CEMZNZEOPZC
      QRZSZQDPZTZBUAZAUAZNZBDUBZAUUMUCZUDPZUUQUUSUEZUVEQUVDPZTZUVDUFZUVDPZUEZGU
      AZUVDPZHUAZUVINZUVLUVNNZSZUVNUVDPZUGZHUHGUHZUVLUVNUIZUVDPZHUVDUJGUVDUJZSZ
      UVFUVKUVTUWCUVFUVHUVJUUSUVHUUQUVGUURUVGQUUMPZUUTQNZBDUBZUEUURUVCUWGAQUUMU
      VAQUKUVBUWFBDUVAQUUTULUMUNUWGUURUWEUWFUURBDUWFUUTDPZUURUWFUUTQDUUTUOUPUQU
      RUSUTVAUSUVFUVIUUMPZUUTUVINZBDUBZUEZUVJUUQUWLUUSUUQUWIUWKUUOUUNUWIUUPUVCA
      EOVBVCUUQDQRZUWJBDUJUWKUUPUUNUWMUUOUUPUUTCNUUTVDPUVAUUTVEUKSBVFAVGZQRUWMA
      BCVHDUWNQFVIVJVKUUQUWJBDUUQUWHUEZUUTUUTNZUUTUVDPZUWJUUTVLZUWOUUTUUMPZIUAZ
      UUTNZIDUBZUWQUWOUUTENUWSUWOUUTUUMUFZEUWHUUQUUTCUFZUXCJUAZDPZUXEUXDNZUGUWH
      UUTUXDNZUGZJBUXIJVMUXEUUTUKUXFUWHUXGUXHUXEUUTDVNUXEUUTUXDVOVPUXFUXEUWNPZU
      XGDUWNUXEFVQZABUXECVRUTVSUUNUUOUXDUXCNUUPCUUMVTWAWBEWCWDUUTEBWEWFVJUWOUWH
      UWPUXBUUQUWHWGUWRUXAUWPIUUTDUWTUUTUUTVOWHWIUVCUXBAUUTUUMUVCUWTUVANZIDUBUV
      AUUTUKZUXBUVBUXLBIDUUTUWTUVAVOWJUXMUXLUXAIDUVAUUTUWTULUMWKUNWLUUTUUTUVDWM
      WNWOUWJBDWPWQWRWSUVCUWKAIUVIUUMAIUVDUVCAIUUMWTXAZAIEUWTEPAVMXBUWJABDUWHAV
      MAIUUTUVIUWTUUTPAVMUXNXCXDUVAUVIUKUVBUWJBDBKIUVAUVIKUAUVAPBVMBIUVDUVCBAIU
      UMUVBBDXEUWTUUMPZBVMXFXAXGUVAUVIUUTULXHXIVJWRUVTUVFUVSGHUVQUVNUUMPZUUTUVN
      NZBDUBZUEZUVRUVMUVLUUMPZUUTUVLNZBDUBZUEZUVOUVPUXSUVCUYBAUVLUUMUVAUVLUKZUV
      BUYABDUVAUVLUUTULUMUNUYCUVOUVPSUXPUXRUVOUYCUXPUVPUVOUVNUVIMZPUXPUVNUVIHWE
      WFUYEUUMUVNUVIENZUYEUUMNUYFUWTENZIUVDIUVDEXJUWTUVDPUXOUUTUWTNZBDUBZUEUYGU
      VCUYIAUWTUUMUVAUWTUKUVBUYHBDUVAUWTUUTULUMUNUXOUYGUYIUWTEXKWSUTXLUVIEXMXNX
      OXPVCUYCUVPUXRUVOUYBUVPUXRUXTUYBUVPUEUYAUVPUEZBDUBUXRUYAUVPBDXQUYJUXQBDUU
      TUVLUVNXRXSXPXTYAWRYBUVCUXRAUVNUUMUVAUVNUKZUVBUXQBDUVAUVNUUTULUMUNVJYCYDU
      WCUVFUWBGHUVDUVDUVMUYCUXSUWBUVRUVCUYBAUVLUUMUYDUVBUYABDUYDUWHUEUVAUVLUUTU
      YDUWHYEYFYGUNUVCUXRAUVNUUMUYKUVBUXQBDUYKUWHUEUVAUVNUUTUYKUWHYEYFYGUNUYCUX
      SUEUWAUUMPZUUTUWANZBDUBZUEZUWBUXTUXPUYBUXRUYOUXTUXPUEUYLUYBUXRUEUYNUXTUYL
      UXPUVLUVNEYHWSUYBUXEUVLNZJDUBZLUAZUVNNZLDUBZUYNUXRUYAUYPBJDUUTUXEUVLVOWJU
      XQUYSBLDUUTUYRUVNVOWJUYQUYTUYNUYPUYTUYNUGJDUYTUXFUYPUEZUYNUYSVUAUYNUGLDVU
      AUYRDPZUYSUEUYNUXFVUBUYPUYSUYNUXFVUBUEZUXEUYRUIZDPZVUDUWANZUYNUYPUYSUEVUC
      UXJUYRUWNPZUEVUDUWNPZVUEUXFUXJVUBVUGUXFUXJUXKYIVUBVUGDUWNUYRFVQYIYJABUXEU
      YRCYKVUHVUEUWNDVUDDUWNFYLVQYIYMUXEUVLUYRUVNYNUYMVUFBVUDDVUFBVMUUTVUDUWAVO
      YOYPYQYRYSYTYSUUAUUBYJYQUVCUYNAUWAUUMUVAUWAUKZUVBUYMBDVUIUWHUEUVAUWAUUTVU
      IUWHYEYFYGUNVJUUBUUCYDUUDUVFUVDOPZUVEUWDUUEUUQVUJUUSUUOUUNVUJUUPUUOUUMOPV
      UJEOUUFUVCAUUMOUUGUUHVCWSGHOUVDUVIUVIUUIUUJUUHUUKUUL $.
      $( [2-Sep-2008] $) $( [16-Nov-2007] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d X x y z $.
    efilcp.1 $e |- B = { z | E. y
                  ( y C_ A /\ y e. Fin /\ z = |^| y ) } $.
    $( A filter containing a set ` A ` exists iff ` A ` has the finite
       intersection property (i.e. no finite intersection of elements of ` A `
       is empty).  Bourbaki TG I.37 prop. 1. $)
    efilcp $p |- ( ( A C_ ~P X /\ X e. _V /\ A =/= (/) ) ->
      ( -. (/) e. B <-> E. x e. Fil A C_ x ) ) $=
      ( cpw wss cvv wcel c0 wne w3a wn cv cfil wrex wa crab fgsb imp wral
      simpl1 cfn cint wceq wex cab abfi sseqtr4i ssel2 ssid sseq1 rcla4ev
      mpan2 syl mpan rgen jctir ssrab sylibr sseq2 syl11anc ex wi sstr df-ne
      fipfil2 pm2.27 necom biimpi sylbir syl6 3exp com34 com4t adantr com14
      com13 com23 imp31 inteq int0 eqeq1i vn0 neeq1 mpbii sylbi eqcoms necomd
      sylib a1d pm2.61d2 nan mpbir df-3an notbii nexdv 0ex eqeq1 3anbi3d
      exbidv elab2 exp31 r19.23aiv com12 impbid ) DFHZIZFJKZDLMZNZLEKZOZDAPZIZA
      QRZXMXOXRXMXOSZBPZCPZIZBERZCXITZQKZDYDIZXRXMXOYECBDEFGUAUBXSXJYCCDUCZSYFX
      SXJYGXJXKXLXOUDYCCDDEIZYADKZYCDXTDIZXTUEKZYAXTUFZUGZNZBUHZCUIECBDUJGUKYHY
      ISYAEKZYCDEYAULYPYAYAIZYCYAUMYBYQBYAEXTYAYAUNUOUPUQURUSUTYCCXIDVAVBXQYFAY
      DQXPYDDVCUOVDVEXRXMXOXQXMXOVFAQXPQKZXQXMXOYRXQSXMSZYJYKLYLUGZNZBUHZOXOYSU
      UABYSYJYKSZYTSZOZUUAOYSUUEVFYSUUCSYTOZVFYSUUCUUFYSXTLUGZUUCUUFVFZYRXQXMUU
      GOZUUHVFZYRXMXQUUJYRXMXQUUJVFUUCXQUUIYRXMSZUUFYJYKXQUUIUUKUUFVFZVFZVFYJXQ
      YKUUMYJXQYKUUMVFZYJXQSXTXPIZUUNXTDXPVGUUIYKUUOUULUUIXTLMZYKUUOUULVFVFXTLV
      HUUKYKUUOUUPUUFYRYKUUOUUPUUFVFVFVFZXMYRUUOUUPYKNZYLLMZVFZUUQXTXPVIUUOUUPU
      UTYKUUFUUOUUPYKUUTUUFUUOUUPYKUUTUUFVFUURUUTUUSUUFUURUUSVJUUSLYLMZUUFLYLVK
      UVAUUFLYLVHZVLVMVNVOVPVQUQVRVSVMVTUQVEWAUBVSVEWAWBUUGUUFUUCUUGUVAUUFUUGYL
      LUUSLXTLXTUGLUFZYLUGZUUSLXTWCUVDJYLUGZUUSUVCJYLWDWEUVEJLMUUSWFJYLLWGWHWIU
      QWJWKUVBWLWMWNUBYSUUCYTWOWPUUAUUDYJYKYTWQWRVBWSXNUUBYOUUBCLEWTYALUGZYNUUA
      BUVFYMYTYJYKYALYLXAXBXCGXDWRVBXEXFXGXH $.
      $( [2-Sep-2008] $) $( [20-Nov-2007] $)
  $}

  ${
    $d A y $.  $d F x y $.
    $( A filter is closed under taking finite intersections. $)
    filint2 $p |- ( F e. Fil -> ( ( A C_ F /\ A =/= (/) /\ A e. Fin )
           -> |^| A e. F ) ) $=
      ( vy vx cfil wcel wss c0 wne cfn cint wi cpw elpw2g cv wceq sseq1 neeq1
      eleq1 inteq eleq1d imbi12d imbi2d w3a wn cuni wa wal cin wral eqid isfil
      simp3 fiint sylib 19.21bi syl6bi pm2.43i 3expd com12 vtoclg syl6bir
      com4l com13 3impd ) BEFZABGZAHIZAJFZAKZBFZVFVGVHVIVKLZLZLVGVFVFVMVGVFVFVM
      LZLVFVGVGVFVMVFVGABMZFVGVNLZABENCOZBGZVFVQHIZVQJFZVQKZBFZLZLZLZLVPCAVOVQA
      PZVRVGWEVNVQABQWFWDVMVFWFVSVHWCVLVQAHRWFVTVIWBVKVQAJSWFWAVJBVQATUAUBUBUCU
      BVFVRWDVFVRVSVTWBVFVRVSVTUDWBLZVFVFHBFUEBUFZBFUGZVQBFDOZWHGVQWJGUDWJBFLDU
      HCUHZVQWJUIBFDBUJCBUJZUDZWGCDEBWHWHUKULWMWGCWMWLWGCUHWIWKWLUMCDBUNUOUPUQU
      RUSUTVAVBVCURVDURVE $.
      $( [2-Sep-2008] $) $( [27-Apr-2008] $)
  $}


  ${
    $d A y z $.  $d B y $.  $d C y z $.
    fisub.1 $e |- B = { z | E. y ( y C_ A /\ y e. Fin /\ z = |^| y ) } $.
    fisub.2 $e |- D = { z | E. y ( y C_ C /\ y e. Fin /\ z = |^| y ) } $.
    $( If a set has the finite intersection property, its subsets have also
       this property. $)
    fisub $p |- ( C C_ A -> ( -. (/) e. B -> -. (/) e. D ) ) $=
      ( wss c0 wcel cv cfn cint wceq w3a wex 0ex eqeq1 3anbi3d exbidv elab2 wi
      wa sstr biimpri 19.23bi 3exp syl expcom com4l 3imp 19.23aiv com12 syl5bi
      con3d ) ECIZJFKZJDKZURALZEIZUTMKZJUTNZOZPZAQZUQUSVAVBBLZVCOZPZAQVFBJFRVGJ
      OZVIVEAVJVHVDVAVBVGJVCSZTUAHUBVFUQUSVEUQUSUCZAVAVBVDVLUQVAVBVDUSVAUQVBVDU
      SUCUCZVAUQUDUTCIZVMUTECUEVNVBVDUSVNVBVDPZUSAUSVOAQZVNVBVHPZAQVPBJDRVJVQVO
      AVJVHVDVNVBVKTUAGUBUFUGUHUIUJUKULUMUNUOUP $.
      $( [2-Sep-2008] $) $( [27-Apr-2008] $)
  $}

  ${
    $d A a b u v y $.  $d A t x y $.  $d A x y z $.  $d X a b x y $.
    $d X t x y $.  $d X x y z $.  $d w x y $.
    $( Filter generated by a subbasis ` A ` .  Bourbaki TG I.37 paragraph above
       prop. 1.  (The theorem has been slightly modified because the
       definitions of the empty set are different in Bourbaki and Metamath.) $)
    fgsb2 $p |- ( ( A C_ ~P X /\ X e. _V /\ A =/= (/) ) ->
      ( -. (/) e. ( fi ` A ) ->
         { x e. ~P X | E. y e. ( fi ` A ) y C_ x } e. Fil ) ) $=
      ( va vb vz vw vt vu vv cpw wss cvv wcel c0 wne w3a cfi cfv wn cv wrex
      crab cfil wa cuni wi wal cin wral wceq sseq2 rexbidv elrab ss0 eleq1d
      biimpcd r19.23aiv adantl sylbi con3i ump 3ad2ant2 simp1 pwexg ssexg
      syl11anc simp3 fine2 sylc ssid fiiu2 syl imp sspwuni sylib adantr sstrd
      visset elpw sylibr simpr sseq1 rcla4ev sylancl cbvrexv syl5bb sylanbrc
      ssuni sylancr r19.21aiva r19.2z jca hbrab1 hbuni ax-17 hbpw hbss hbrex
      hbre1 hbrab hbeq rexbid elrabf unissb elpwi mprgbir sspwb mpbi sseli
      sylbir r19.41v sstr reximi adantll 3adant2 syl3an1b gen2 a1i simpl
      sseq2d rexbidva inpws1 cdm elfvdm infi exp3a mpcom ss2in rcla4e syl2an
      an4s expcom r19.23aiva com12 syl2anb anim12i rgen2 3jca wb rabexg eqid
      isfil mpbird ex ) CDLZMZDNOZCPQZRZPCSTZOZUAZBUBZAUBZMZBUULUCZAUUGUDZUEOZU
      UKUUNUFZUUTPUUSOZUAZUUSUGZUUSOZUFZEUBZUUSOZFUBZUVDMZUVGUVIMZRZUVIUUSOZUHZ
      FUIEUIZUVGUVIUJZUUSOZFUUSUKEUUSUKZRZUVAUVFUVOUVRUVAUVCUVEUUNUVCUUKUVBUUMU
      VBPUUGOZUUOPMZBUULUCZUFUUMUURUWBAPUUGUUPPULUUQUWABUULUUPPUUOUMUNUOUWBUUMU
      VTUWAUUMBUULUWAUUOUULOZUUMUWAUUOPUULUUOUPUQURUSUTVAVBUTUVAUVDUUGOZUUOUVDM
      ZBUULUCZUFZUVEUUKUWGUUNUUKUWDUWFUUIUUHUWDUUJUURADNVCVDUUKUULPQZUWEBUULUKU
      WFUUKCNOZUUJUWHUUKUUHUUGNOZUWIUUHUUIUUJVEZUUIUUHUWJUUJDNVFZVDZCUUGNVGZVHU
      UHUUIUUJVICNVJVKUUKUWEBUULUUKUWCUFZUUOUUOMZUUOUUSOZUWEUUOVLZUWOUUOUUGOZGU
      BZUUOMZGUULUCZUWQUWOUUODMUWSUWOUUOCUGZDUUKUWCUUOUXCMZUUKUUHUWJUWCUXDUHZUW
      KUWMUUHUWJUFUWIUXEUWNUUOCNVMVNVHVOUUKUXCDMZUWCUUKUUHUXFUWKCDVPVQVRVSUUODB
      VTWAWBUWOUWCUWPUXBUUKUWCWCUWRUXAUWPGUUOUULUWTUUOUUOWDWEWFUURUXBAUUOUUGUUR
      UWTUUPMZGUULUCUUPUUOULZUXBUUQUXGBGUULUUOUWTUUPWDWGUXHUXGUXAGUULUUPUUOUWTU
      MUNWHUOWIUUOUUOUUSWJWKWLUWEBUULWMVHWNVRUURUWFAGUVDUUGAGUUSUURAGUUGWOWPZAG
      DUWTDOAWQWRUWEABUULUWCAWQAGUUOUVDUWTUUOOAWQUXIWSWTUUPUVDULUUQUWEBUULBHGUU
      PUVDHUBUUPOBWQBGUUSUURBAGUUGUUQBUULXAUWTUUGOBWQXBWPXCUUPUVDUUOUMXDXEWBWNU
      VOUVAUVNEFUVLUVIUUGOZUUOUVIMZBUULUCZUFZUVMUVHUVGUUGOZUUOUVGMZBUULUCZUFZUV
      JUVKUXMUURUXPAUVGUUGUUPUVGULZUUQUXOBUULUUPUVGUUOUMUNUOUXQUVJUVKRUXJUXLUVJ
      UXQUXJUVKUVJUVIUVDLZOUXJUVIUVDFVTWAUXSUUGUVIUVDDMZUXSUUGMUXTIUBZDMZIUUSIU
      USDXFUYAUUSOUYAUUGOZUUOUYAMZBUULUCZUFUYBUURUYEAUYAUUGUUPUYAULUUQUYDBUULUU
      PUYAUUOUMUNUOUYCUYBUYEUYADXGVRVAXHUVDDXIXJXKXLVDUXQUVKUXLUVJUXPUVKUXLUXNU
      XPUVKUFUXOUVKUFZBUULUCUXLUXOUVKBUULXMUYFUXKBUULUUOUVGUVIXNXOXLXPXQWNXRUUR
      UXLAUVIUUGUUPUVIULZUUQUXKBUULUUPUVIUUOUMUNUOWBXSXTUVRUVAUVQEFUUSUUSUVHUXQ
      UXMUVQUVMUURUXPAUVGUUGUXRUUQUXOBUULUXRUWCUFUUPUVGUUOUXRUWCYAYBYCUOUURUXLA
      UVIUUGUYGUUQUXKBUULUYGUWCUFUUPUVIUUOUYGUWCYAYBYCUOUXQUXMUFUVPUUGOZUUOUVPM
      ZBUULUCZUFZUVQUXNUXJUXPUXLUYKUXNUXJUFUYHUXPUXLUFUYJUXNUYHUXJUVGUVIDYDVRUX
      PJUBZUVGMZJUULUCZKUBZUVIMZKUULUCZUYJUXLUXOUYMBJUULUUOUYLUVGWDWGUXKUYPBKUU
      LUUOUYOUVIWDWGUYNUYQUYJUYMUYQUYJUHJUULUYQUYLUULOZUYMUFZUYJUYPUYSUYJUHKUUL
      UYSUYOUULOZUYPUFUYJUYRUYTUYMUYPUYJUYRUYTUFUYLUYOUJZUULOZVUAUVPMZUYJUYMUYP
      UFUYRUYTVUBCSYEZOZUYRUYTVUBUHUYLCSYFVUEUYRUYTVUBUYLUYOCVUDYGYHYIVOUYLUVGU
      YOUVIYJUYIVUCBVUAUULVUCBWQUUOVUAUVPWDYKYLYMYNYOYPYOVOYQYRYMUURUYJAUVPUUGU
      UPUVPULZUUQUYIBUULVUFUWCUFUUPUVPUUOVUFUWCYAYBYCUOWBYQYSXTYTUVAUUSNOZUUTUV
      SUUAUUKVUGUUNUUIUUHVUGUUJUUIUWJVUGUWLUURAUUGNUUBVNVDVREFNUUSUVDUVDUUCUUDV
      NUUEUUF $.
      $( [27-Apr-2008] $)
  $}

  ${
    $d A x y z $.  $d X x y z $.
    $( A filter containing a set ` A ` exists iff ` A ` has the finite
       intersection property (i.e. no finite intersection of elements of ` A `
       is empty).  Bourbaki TG I.37 prop. 1. $)
    efilcp2 $p |- ( ( A C_ ~P X /\ X e. _V /\ A =/= (/) ) ->
        ( -. (/) e. ( fi ` A ) <-> E. x e. Fil A C_ x ) ) $=
      ( vy vz cpw wss cvv wcel c0 wne w3a cfi cfv wn cv cfil wrex wa crab
      fgsb2 imp wral simpl1 wi wb pwexg elpw2g syl biimprd elisset a1d syl6com
      3imp adantr abfi2 ssel2 ssid sseq1 rcla4ev mpan2 r19.21aiva jca ssrab
      sylibr sseq2 syl11anc ex cfn cint wceq wex sstr df-ne fipfil2 pm2.27
      necom biimpi sylbir syl6 3exp com34 com4t com14 com13 com23 inteq int0
      eqeq1i vn0 neeq1 mpbii sylbi eqcoms necomd sylib pm2.61d2 nan mpbir
      df-3an notbii nexdv 0ex sppfi mpan notbid syl5ibr 3expd com24 mpcom
      r19.23aiv com12 impbid ) BCFZGZCHIZBJKZLZJBMNZIZOZBAPZGZAQRZXRYAYDXRYASZD
      PZEPZGZDXSRZEXNTZQIZBYJGZYDXRYAYKEDBCUAUBYEXOYIEBUCZSYLYEXOYMXOXPXQYAUDYE
      BHIZYMXRYNYAXOXPXQYNXPXOBXNFZIZXQYNUEXPYPXOXPXNHIYPXOUFCHUGBXNHUHUIUJYPYN
      XQBYOUKULUMUNZUOYNBXSGZYMBHUPYRYIEBYRYGBISYGXSIZYIBXSYGUQYSYGYGGZYIYGURYH
      YTDYGXSYFYGYGUSUTVAUIVBUIUIVCYIEXNBVDVEYCYLAYJQYBYJBVFUTVGVHYDXRYAYCXRYAU
      EAQXRYCYBQIZYAYNXRYCUUAYAUEUEYQYNUUAYCXRYAYNUUAYCXRYAUUAYCXRLZYAYNYFBGZYF
      VIIZJYFVJZVKZLZDVLZOUUBUUGDUUBUUCUUDSZUUFSZOZUUGOUUBUUKUEUUBUUISUUFOZUEUU
      BUUIUULUUBYFJVKZUUIUULUEZUUAYCXRUUMOZUUNUEZUUAXRYCUUPUUAXRYCUUPUEUUIYCUUO
      UUAXRSZUULUUCUUDYCUUOUUQUULUEZUEZUEUUCYCUUDUUSUUCYCUUDUUSUEZUUCYCSYFYBGZU
      UTYFBYBVMUUOUUDUVAUURUUOYFJKZUUDUVAUURUEUEYFJVNUUQUUDUVAUVBUULUUAUUDUVAUV
      BUULUEUEUEZXRUUAUVAUVBUUDLZUUEJKZUEZUVCYFYBVOUVAUVBUVFUUDUULUVAUVBUUDUVFU
      ULUVAUVBUUDUVFUULUEUVDUVFUVEUULUVDUVEVPUVEJUUEKZUULJUUEVQUVGUULJUUEVNZVRV
      SVTWAWBWCUIUOWDVSWEUIVHWFUBWDVHWFUNUUMUULUUIUUMUVGUULUUMUUEJUVEJYFJYFVKJV
      JZUUEVKZUVEJYFWGUVJHUUEVKZUVEUVIHUUEWHWIUVKHJKUVEWJHUUEJWKWLWMUIWNWOUVHWP
      ULWQUBUUBUUIUUFWRWSUUGUUJUUCUUDUUFWTXAVEXBYNXTUUHJHIYNXTUUHUFXCDJHBHXDXEX
      FXGXHXIXJWEXKXLXM $.
      $( [2-Sep-2008] $) $( [27-Apr-2008] $)
  $}

  ${
    $d A g $.  $d A x y $.  $d F g $.  $d F x y $.
    $( Condition to have a filter finer than a given filter and containing a
       set ` A ` .  Bourbaki T.G. I.37 cor. 1 $)
    cnfilca $p |- ( ( F e. Fil /\ A C_ U. F /\ A =/= (/) )
      -> ( E. g e. Fil ( A e. g /\ F C_ g )
         <-> A. x e. F ( x i^i A ) =/= (/) ) ) $=
      ( vy cfil wcel cuni wss c0 wne w3a cv wa wrex csn cun cfi cfv wn cin
      wral cvv wb uniexg ssexg sylan2 ancoms 3adant3 snssg anbi1d unss syl6bb
      syl rexbidv cpw wi ex elpwg bitr3d pwuni biimpi mpan2 syl6bi syl6
      pm2.43a mpan9 3ad2ant1 emnfil wceq nelneq simpr con3i un00 necon3bbii
      sylib efilcp2 syl111anc cfn cint wex 0ex elisset snex jctil unexb sppfi
      notbid sylancr wal alnex df-3an notbii imnan bitr4i albii bitr3i ax-17
      cpr elun2 adantl snidg elun1 adantr jca simp2 mpbird prssg bicomd
      syl11anc prfi jctir prex a1i sseq1 eleq1 anbi12d inteq eqeq2d imbi12d
      cla4gv mpid com23 imp31 df-ne sylibr necomd visset ad2antrr intprg
      eqcomd neeq1d r19.21ai cdif moec ssundif int0 eqtr ineq2 inv1 neeq1
      necom sylbi eqcoms com3l mpi syl6com mpsyl com12 com14 3ad2ant3 imp
      difss ssfi simp1 filint2 ineq1 rcla4v incom biimpd ax-mp 3expd com13
      sylbir 3imp com3r pm2.61i elsni con3d 19.21aiv disj1 disjssun vn0 necomi
      neeq2 mpbii fipfil2 3exp2 pm2.61ine com24 impbida syl5bb bitrd 3bitr2d )
      DFGZBDHZIZBJKZLZBCMZGZDUWJIZNZCFOBPZDQZUWJIZCFOZJUWORSGZTZAMZBUAZJKZADUBZ
      UWIUWMUWPCFUWIBUCGZUWMUWPUDUWEUWGUXDUWHUWGUWEUXDUWEUWGUWFUCGZUXDDFUEZBUWF
      UCUFZUGUHUIZUXDUWMUWNUWJIZUWLNUWPUXDUWKUXIUWLBUWJUCUJUKUWNDUWJULUMUNUOUWI
      UWOUWFUPZIZUXEUWOJKZUWSUWQUDUWEUWGUXKUWHUWEUXEUWGUXKUXFUXEUWGUXKUWGUXEUXD
      UWGUXKUQUWGUXEUXDUXGURUXDUWGUWNUXJIZUXKUXDBUXJGZUWGUXMBUWFUCUSZBUXJUCUJUT
      UXMDUXJIZUXKDVAUXMUXPNUXKUWNDUXJULVBVCVDVEVFVGUIUWEUWGUXEUWHUXFVHUWEUWGUX
      LUWHUWEJFGTZUXLVIUWEUXQNDJVJZTZUXLDJFVKUXSUWNJVJZUXRNZTUXLUYAUXRUXTUXRVLV
      MUYAUWOJUWNDVNVOVPUNVCVHCUWOUWFVQVRUWIUWSEMZUWOIZUYBVSGZJUYBVTZVJZLZEWAZT
      ZUXCUWIJUCGZUWOUCGZUWSUYIUDWBUWIUWNUCGZDUCGZNUYKUWIUYMUYLUWEUWGUYMUWHDFWC
      VHBWDWEUWNDWFVPUYJUYKNUWRUYHEJUCUWOUCWGWHWIUYIUYCUYDNZUYFTZUQZEWJZUWIUXCU
      YIUYGTZEWJUYQUYGEWKUYRUYPEUYRUYNUYFNZTUYPUYGUYSUYCUYDUYFWLWMUYNUYFWNWOWPW
      QUWIUYQUXCUWIUYQNZUXBADUYTAWRUYTUWTDGZUXBUYTVUANZUXBUWTBWSZVTZJKVUBJVUDVU
      BJVUDVJZTZJVUDKUWIUYQVUAVUFUWIVUAUYQVUFUWIVUAUYQVUFUQUWIVUANZUYQVUCUWOIZV
      UCVSGZNZVUFVUGVUHVUIVUGVUHUWTUWOGZBUWOGZNZVUGVUKVULVUAVUKUWIUWTDUWNWTXAUW
      IVULVUAUWIUXDVULUXHUXDBUWNGVULBUCXBBUWNDXCUNUNXDXEVUGVUAUXNVUHVUMUDUWIVUA
      VLUWIUXNVUAUWIUXNUWGUWEUWGUWHXFUWIUXDUXNUWGUDUXHUXOUNXGXDVUAUXNNVUMVUHUWT
      BUWODUXJXHXIXJXGUWTBXKXLVUGVUCUCGZUYQVUJVUFUQZUQVUNVUGUWTBXMXNUYPVUOEVUCU
      CUYBVUCVJZUYNVUJUYOVUFVUPUYCVUHUYDVUIUYBVUCUWOXOUYBVUCVSXPXQVUPUYFVUEVUPU
      YEVUDJUYBVUCXRXSWHXTYAUNYBURYCYDJVUDYEYFYGVUBUXAVUDJVUBVUDUXAVUBUWTUCGUXD
      VUDUXAVJAYHUWIUXDUYQVUAUXHYIUWTBUCUCYJWIYKYLXGURYMUWIUXCNZUYPEBUYBGZVUQUY
      PUQZVURUYEBUYBUWNYNZVTZUAZVJZVUSUYBBYOUYNVUQVVCUYOUYCUYDVUQVVCUYOUQZUQZUY
      CVUTDIZUYDVVEUQZUYBUWNDYPVUTJVJZVVFVVGUQZVUQVVFUYDVVHVVDUWIUXCVVFUYDVVHVV
      DUQZUQUQZUWHUWEUXCVVKUQUWGUYDUXCVVFUWHVVJUXCVVFUWHVVJUQZUQZUQUYDVVMUXCVVL
      VVFVVHUWHVVDJVTZUCVJZVVHVVAVVNVJZUWHVVDUQZYQVUTJXRVVPVVOVVAUCVJZVVQVVPVVO
      VVRVVAVVNUCYRURVVRVVBBUCUAZVJZVVQVVAUCBYSVVTVVSBVJZVVQBYTVVTVWAVVBBVJZVVQ
      VVTVWAVWBVVBVVSBYRURVVCVWBUWHUYOVVCVWBUYEBVJZUWHUYOUQZVVCVWBVWCUYEVVBBYRU
      RVWDBUYEBUYEVJUWHUYEJKZUYOBUYEJUUAVWEJUYEKZUYOUYEJUUBVWFUYOJUYEYEZVBUUCZV
      DUUDVEUUEVEUUFUNUUGUUHUUIXNXNXNUUJUUKUULUUJVVHTZVUTUYBIZVVIUYBUWNUUMUYDVW
      JVVFVWIVVEUYDVWJVUTVSGZVVFVWIVVEUQUQUYDVWJVWKUYBVUTUUNURVUQVVFVWIVWKVVDUW
      IUWEUXCVVFVWIVWKVVDUQZUQUQUWEUWGUWHUUOUXCUWEVVFVWIVWLUWEVVFVWILVWKUXCVVDU
      WEVVFVWIVWKUXCVVDUQZUQZVWIVVFUWEVWNVWIVUTJKZVVFUWEVWNUQUQVUTJYEUWEVVFVWOV
      WNUWEVVFVWOVWKVWMUWEVVFVWOVWKLVVADGZVWMVUTDUUPVWPUXCVVABUAZJKZVVDUXBVWRAV
      VADUWTVVAVJUXAVWQJUWTVVABUUQYLUURVWQVVBVJZVWRVVDUQVVABUUSVWSVWRVVBJKZVVDV
      WQVVBJUUAVVCVWTVWEUYOVWTVWEUQVVBUYEVVBUYEVJVWTVWEVVBUYEJUUAUUTUUDVWHUUGVD
      UVAVEVEUVBUVCUVDUVCUVEUVFUVBVGUUJVEUUJUUFUVGUUCUULUVCUNUYNVUQVURTZUYOUYCU
      YDVUQVXAUYOUQUQUYCVXAVUQUYDUYOVXAUYCUYBDIZVUQUYDUYOUQZUQVXAUYBUWNUAJVJZUY
      CVXBUQVXAUWTUYBGZUWTUWNGZTZUQZAWJVXDVXAVXHAVXEVXAVXGVXEVXFVURVXFVXEVURVXF
      UWTBVJZVXEVURUQUWTBUVHVXIVXEVURUWTBUYBXPUUTUNUUIUVIUUIUVJAUYBUWNUVKYFVXDU
      YCVXBUYBUWNDUVLUUTUNVUQVXBVXCUWIVXBVXCUQZUXCUWEUWGVXJUWHUWEVXJUQUYBJUYDUW
      EVXBUYBJVJZUYOUWEVXBVXKUYOUQZUQZUQUYDVXMUWEVXLVXBVXKUYEVVNVJZUYOUYBJXRVXN
      VVOUYOYQVXNVVOUYEUCVJZUYOVXNVVOVXOUYEVVNUCYRURUYOUCUYEUCUYEVJZVWFUYOVXPJU
      CKVWFUCJUVMUVNUCUYEJUVOUVPVWGVPUUDVEUUFUNXNXNXNUUJUWEVXBUYBJKZVXCUWEVXBVX
      QUYDUYOUWEVXBVXQUYDLZNZVWFUYOVXSUYEJUWEVXRVWEUYBDUVQUULYGVWGVPUVRUVFUVSVH
      XDUUIUUGUVTUULUVCUVGUVJUWAUWBUWCUWD $.
      $( [2-Sep-2008] $) $( [27-Apr-2008] $)
  $}

  ${
    $d A x $.  $d B b x $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem1 $p |- ( B e. C
        -> ( B e. { x | E. b ( b C_ A /\ b e. Fin /\ x = ( A \ b ) ) }
                 <-> E. b ( b C_ A /\ b e. Fin /\ B = ( A \ b ) ) ) ) $=
      ( cv wss cfn wcel cdif wceq w3a wex eqeq1 3anbi3d exbidv elabg ) EFZBGZRH
      IZAFZBRJZKZLZEMSTCUBKZLZEMACDUACKZUDUFEUGUCUESTUACUBNOPQ $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b x $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem2 $p |- ( -. A e. Fin -> -. (/) e. { x | E. b
        ( b C_ A /\ b e. Fin /\ x = ( A \ b ) ) } ) $=
      ( cfn wcel wn cv wss c0 cdif wceq w3a wex cab wi ssdif0 eqcom bitri wa
      eqss eleq1 biimpd sylbir ex com23 com13 3imp 19.23aiv con3i cvv wb 0ex
      rcfpfillem1 notbid ax-mp sylibr ) BDEZFCGZBHZURDEZIBURJZKZLZCMZFZIUSUTAGV
      AKLCMANEZFZVDUQVCUQCUSUTVBUQVBUTUSUQVBBURHZUTUSUQOOVHVAIKVBBURPVAIQRVHUSU
      TUQVHUSUTUQOZVHUSSZURBKZVIVKBURKVJURBQBURTRVKUTUQURBDUAUBUCUDUEUCUFUGUHUI
      IUJEZVGVEUKULVLVFVDABIUJCUMUNUOUP $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b x y $.  $d V b y $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem3 $p |- ( A e. V -> U. { x | E. b
         ( b C_ A /\ b e. Fin /\ x = ( A \ b ) ) } = A ) $=
      ( vy wcel cv wss cfn cdif wceq w3a wex cab cuni wa eluniab wi 19.42v
      bicomi exbii excom simpr3 simpl jca 3simpa adantl eximi 19.40 cvv difexg
      eleq2 ceqsexgv eldifi syl6bi syl com12 adantr 3syl 19.23aiv sylbi eqeq1
      3anbi3d exbidv anbi12d ancom c0 0ex 0ss 0fin dif0 eqcomi 3pm3.2i sseq1
      eleq1 difeq2 eqeq2d 3anbi123d cla4egv mp2 mpbiran syl6bb impbid syl5bb
      eqrdv ) BCFZEDGZBHZWGIFZAGZBWGJZKZLZDMZANOZBEGZWOFWPWJFZWNPZAMZWFWPBFZWNA
      WPQWFWSWTWSWFWTWSWQWMPZDMZAMZWFWTRZWRXBAXBWRWQWMDSTUAXCXAAMZDMXDXAADUBXEX
      DDXEWLWQPZWHWIPZPZAMXFAMZXGAMZPXDXAXHAXAXFXGXAWLWQWQWHWIWLUCWQWMUDUEWMXGW
      QWHWIWLUFUGUEUHXFXGAUIXIXDXJWFXIWTWFWKUJFZXIWTRBWGCUKXKXIWPWKFZWTWQXLAWKU
      JWJWKWPULUMWPBWGUNUOUPUQURUSUTVAVAUQWRWTABCWJBKZWRWTWHWIBWKKZLZDMZPZWTXMW
      QWTWNXPWJBWPULXMWMXODXMWLXNWHWIWJBWKVBVCVDVEXQXPWTWTXPVFVGUJFVGBHZVGIFZBB
      VGJZKZLZXPVHXRXSYABVIVJXTBBVKVLVMXOYBDVGUJWGVGKZWHXRWIXSXNYAWGVGBVNWGVGIV
      OYCWKXTBWGVGBVPVQVRVSVTWAWBVSWCWDWE $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A a b c u v x $.  $d V u v $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem4 $p |- ( A e. V
        -> A. u e. { x | E. b ( b C_ A /\ b e. Fin /\ x = ( A \ b ) ) }
           A. v e. { x | E. b ( b C_ A /\ b e. Fin /\ x = ( A \ b ) ) }
       ( u i^i v ) e. { x | E. b ( b C_ A /\ b e. Fin /\ x = ( A \ b ) ) } ) $=
      ( va vc wcel cv cin wss cfn cdif wceq w3a wex cab wa cvv wb visset
      rcfpfillem1 bi2anan9 mp2an sseq1 eleq1 difeq2 eqeq2d 3anbi123d cbvexv wi
      cun an6 unss biimpi unfi ineq12 difundi syl6eqr 3anim123i sylbi expcom
      unex cla4ev syl6 19.23aiv com12 imp syl2anb adantl inex1 ax-mp sylibr ex
      syl5bi r19.21aivv ) DEIZCJZBJZKZFJZDLZWBMIZAJDWBNZOPFQARZIZCBWFWFVSWFIZVT
      WFIZSZWCWDVSWEOZPZFQZWCWDVTWEOZPZFQZSZVRWGVSTIZVTTIZWJWQUACUBZBUBWRWHWMWS
      WIWPADVSTFUCADVTTFUCUDUEVRWQWGVRWQSWCWDWAWEOZPZFQZWGWQXCVRWMGJZDLZXDMIZVS
      DXDNZOZPZGQZHJZDLZXKMIZVTDXKNZOZPZHQZXCWPWLXIFGWBXDOZWCXEWDXFWKXHWBXDDUFW
      BXDMUGXRWEXGVSWBXDDUHUIUJUKWOXPFHWBXKOZWCXLWDXMWNXOWBXKDUFWBXKMUGXSWEXNVT
      WBXKDUHUIUJUKXJXQXCXIXQXCULGXQXIXCXPXIXCULHXPXIXDXKUMZDLZXTMIZWADXTNZOZPZ
      XCXIXPYEXIXPSXEXLSZXFXMSZXHXOSZPYEXEXFXHXLXMXOUNYFYAYGYBYHYDYFYAXDXKDUOUP
      XDXKUQYHWAXGXNKYCVSXGVTXNURDXDXKUSUTVAVBVCXBYEFXTXDXKGUBHUBVDWBXTOZWCYAWD
      YBXAYDWBXTDUFWBXTMUGYIWEYCWAWBXTDUHUIUJVEVFVGVHVGVIVJVKWATIWGXCUAVSVTWTVL
      ADWATFUCVMVNVOVPVQ $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b x $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem5 $p |- ( A e. V
         -> A e. { x | E. b ( b C_ A /\ b e. Fin /\ x = ( A \ b ) ) } ) $=
      ( wcel cv wss cfn cdif wceq w3a wex cab c0 cvv 0ex 0ss 0fin cin in0
      disj3 mpbi 3pm3.2i sseq1 eleq1 difeq2 eqeq2d 3anbi123d cla4egv mp2
      rcfpfillem1 mpbiri ) BCEBDFZBGZUMHEZAFBUMIZJKDLAMEUNUOBUPJZKZDLZNOENBGZNH
      EZBBNIZJZKZUSPUTVAVCBQRBNSNJVCBTBNUAUBUCURVDDNOUMNJZUNUTUOVAUQVCUMNBUDUMN
      HUEVEUPVBBUMNBUFUGUHUIUJABBCDUKUL $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b c u v x $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem6 $p |- ( ( u e. { x | E. b ( b C_ A /\ b e. Fin
                 /\ x = ( A \ b ) ) } /\ v C_ A /\ u C_ v )
           -> v e. { x | E. b ( b C_ A /\ b e. Fin /\ x = ( A \ b ) ) } ) $=
      ( vc cv wss cfn wcel cdif wceq w3a wex cab wi cvv wb visset rcfpfillem1
      sseq1 eleq1 difeq2 eqeq2d 3anbi123d cbvexv syl6bb ax-mp ssdifss 3ad2ant1
      difss ssfi mpan2 3ad2ant2 dfss4 wa eqcomd eqeq1d biimpd difeq1 cun
      difun1 undif sylbi syl5eqr syl5ibcom syl6com a1d 3imp impcom difeq2d ex
      com13 eqcoms com12 syl5bi difexg cla4ev syl111anc 3exp 19.23aiv eqeq1
      3anbi3d exbidv elab sylibr ) CGZEGZDHZWHIJZAGZDWHKZLZMZENZAOZJZBGZDHZWGWR
      HZMWIWJWRWLLZMZENZWRWPJWQWSWTXCWQFGZDHZXDIJZWGDXDKZLZMZFNZWSWTXCPPZWGQJZW
      QXJRCSXLWQWIWJWGWLLZMZENXJADWGQETXNXIEFWHXDLZWIXEWJXFXMXHWHXDDUAWHXDIUBXO
      WLXGWGWHXDDUCUDUEUFUGUHXIXKFXIWSWTXCXIWSWTMXDWRWGKZKZDHZXQIJZWRDXQKZLZXCX
      IWSXRWTXEXFXRXHXDDXPUIUJUJXIWSXSWTXFXEXSXHXFXQXDHXSXDXPUKXDXQULUMUNUJXIWS
      WTYAWSDDWRKZKZWRLZXIWTYAPZWRDUOYDXIYEXIYEPWRYCWTXIWRYCLZYAWTXIYFYAPWTXIUP
      ZYFYAYGYCXTWRYGYBXQDXIWTYBXQLZXEXFXHWTYHPZXEDXGKZXDLZXFXHYIPZPXDDUOYKYLXF
      XHYKDWGKZXDLZYIXHYKYNXHYJYMXDXHYMYJWGXGDUCUQURUSYNYMXPKZXQLWTYHYMXDXPUTWT
      YOYBXQWTYODWGXPVAZKZYBDWGXPVBWTYPWRLYQYBLWGWRVCYPWRDUCVDVEURVFVGVHVDVIVJV
      KUDUSVLVMVNVOVPVIXBXRXSYAMEXQXDQJXQQJFSXDXPQVQUHWHXQLZWIXRWJXSXAYAWHXQDUA
      WHXQIUBYRWLXTWRWHXQDUCUDUEVRVSVTWAVDVIWOXCAWRBSWKWRLZWNXBEYSWMXAWIWJWKWRW
      LWBWCWDWEWF $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b u v x y $.  $d V b u v x $.
    $( Relative complements of the finite parts of an infinite set is a
       filter.  When ` A = NN ` the set of the relative complements is called
       Frechet's filter and is used to define the concept of limit of a
       sequence. $)
    rcfpfil $p |- ( ( A e. V /\ -. A e. Fin )
          -> { x | E. b ( b C_ A /\ b e. Fin /\ x = ( A \ b ) ) } e. Fil ) $=
      ( vu vv vy wcel cfn wn wa cv wss cdif wceq w3a wex cab cfil c0 cuni wi
      wal cin wral rcfpfillem2 adantl rcfpfillem3 rcfpfillem5 eqeltrd adantr
      jca unissb visset eqeq1 3anbi3d exbidv elab difss sseq1 mpbiri 3ad2ant3
      19.23aiv sylbi mprgbir sstr2 mpi rcfpfillem6 syl3an2 a1i 19.21aivv
      rcfpfillem4 3jca wb cvv copab crn rnopab cpw elpw2g bicomd 3anbi1d
      opabbidv pwexg opabex2g syl 3simpb ssopab2i jctil ssexg rnexg syl5eqelr
      eqid isfil mpbird ) BCHZBIHJZKZDLZBMZWSIHZALZBWSNZOZPZDQZARZSHZTXGHJZXGUA
      ZXGHZKZELZXGHZFLZXJMZXMXOMZPXOXGHZUBZFUCEUCZXMXOUDXGHFXGUEEXGUEZPZWRXLXTY
      AWRXIXKWQXIWPABDUFUGWPXKWQWPXJBXGABCDUHABCDUIUJUKULWRXSEFXSWRXPXNXOBMZXQX
      RXPXJBMZYCYDGLZBMZGXGGXGBUMYEXGHWTXAYEXCOZPZDQZYFXFYIAYEGUNXBYEOZXEYHDYJX
      DYGWTXAXBYEXCUOUPUQURYHYFDYGWTYFXAYGYFXCBMBWSUSYEXCBUTVAVBVCVDVEXOXJBVFVG
      AFEBDVHVIVJVKWPYAWQAFEBCDVLUKVMWPXHYBVNZWQWPXGVOHYKWPXGXEDAVPZVQZVOXEDAVR
      WPYLVOHYMVOHWPYLWSBVSZHZXAXDPZDAVPZVOWPXEYPDAWPWTYOXAXDWPYOWTWSBCVTWAWBWC
      WPYQYOXDKZDAVPZMZYSVOHZKYQVOHWPUUAYTWPYNVOHUUABCWDDAYNXCVOWEWFYPYRDAYOXAX
      DWGWHWIYQYSVOWJWFUJYLVOWKWFWLEFVOXGXJXJWMWNWFUKWO $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  $( An element of a filter can't be empty. $)
  elfilnemp $p |- ( ( F e. Fil /\ A e. F ) -> A =/= (/) ) $=
    ( cfil wcel c0 wn wne filesn wa wceq nelneq df-ne sylibr ex mpan9 ) BCDEBDF
    ZABDZAEGZBHQPRQPIAEJFRAEBKAELMNO $.
    $( [24-May-2011] $)


  ${
    $d A n o $.  $d B n o x $.  $d F o $.  $d J n o $.  $d N n o $.
    $d X n o $.  $d Y n o $.
    fbaslim2.1 $e |- X = U. J $.
    fbaslim2.2 $e |- Y = U. B $.
    fbaslim2.3 $e |- F = ( filGen ` B ) $.
    fbaslim2.4 $e |- N = ( ( nei ` J ) ` { A } ) $.
    $( A condition for a filter base ` B ` to converge to a point ` A ` .  Use
       neighborhoods instead of open neighborhoods.  Compare ~ fbaslim . $)
    fbaslim2 $p |- ( ( ( J e. Top /\ B e. fBas /\ X = Y ) /\ A e. X ) -> ( A e.
    ( ( fLim1 ` J ) ` F ) <-> A. n e. N E. x e. B x C_ n ) ) $=
      ( vo ctop wcel cfbas wceq w3a wa cflim1 cfv cv wss wrex wi wral fbaslim
      csn cnei eleq2 isneip r19.29r pm2.27 r19.41v sstr reximi sylbir ex syl6
      com23 imp31 r19.23aivw syl expcom adantl com12 syl6bi 3ad2antl1 anabsi5
      ax-mp r19.21aiv wal df-ral weq eleq1 sseq2 rexbidv imbi12d cbvalv
      opnneip imbi1d biimpd syl5com 3exp com34 3ad2ant1 adantr alimdv syl6ibr
      sylbi impbid bitrd ) FOPZCQPZHIRZSZBHPZTZBEFUAUBUBPBNUCZPZAUCZWTUDZACUEZU
      FZNFUGZXBDUCZUDZACUEZDGUGZABCNEFHIJKLUHWSXFXJWSXFXJWSXFTZXIDGXGGPZXKXIGBU
      IFUJUBUBZRZXLXKXIUFZUFMXNXLXGXMPZXOGXMXGUKXKXPXIWSXFXPXIUFWSXPXKXIWNWOWRX
      PXOUFWPWNWRTXPXGHUDZXAWTXGUDZTZNFUEZTXOBNFXGHJULXTXOXQXKXTXIXFXTXIUFWSXTX
      FXIXTXFTXSXETZNFUEXIXSXENFUMYAXINFXAXRXEXIXAXEXRXIXAXEXDXRXIUFXAXDUNXDXRX
      IXDXRTXCXRTZACUEXIXCXRACUOYBXHACXBWTXGUPUQURUSUTVAVBVCVDVEVFVGVFVHVIVAVJV
      GVHVKVGVLUSXJWSXFXJXLXIUFZDVMZWSXFUFZXIDGVNYDWTGPZXDUFZNVMZYEYCYGDNDNVOZX
      LYFXIXDXGWTGVPYIXHXCACXGWTXBVQVRVSVTWSYHXFWSYHWTFPZXEUFZNVMXFWSYGYKNWQYGY
      KUFZWRWNWOYLWPWNYJYGXEWNYJXAYGXDWNYJXAYGXDUFWNYJXASWTXMPZYGXDBFWTWAXNYGYM
      XDUFZUFMXNYGYNXNYFYMXDGXMWTUKWBWCVKWDWEWFVAWGWHWIXENFVNWJVGWKWKVGWLWM $.
      $( [8-Aug-2011] $) $( [4-Jul-2011] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d X x y $.  $d Y x y $.
    fbfgss2.1 $e |- X = U. F $.
    fbfgss2.2 $e |- Y = U. G $.
    $( A condition for a filter to be finer than another filter.  Compare
       ~ fbfgss . $)
    fbfgss2 $p |- ( ( F e. Fil /\ G e. Fil /\ X = Y ) ->
      ( F C_ G <-> A. x e. F E. y e. G y C_ x ) ) $=
      ( cfbas wcel wceq w3a cfil wss cv wrex wral wb filfbas id 3anim123i cfg
      cfv fbfgss fgid 3ad2ant1 3ad2ant2 sseq12d bibi1d syl5ibcom mpcom ) CIJZDI
      JZEFKZLZCMJZDMJZUNLZCDNZBOAONBDPACQZRZUPULUQUMUNUNCSDSUNTUAUOCUBUCZDUBUCZ
      NZUTRURVAABCDEFGHUDURVDUSUTURVBCVCDUPUQVBCKUNCUEUFUQUPVCDKUNDUEUGUHUIUJUK
      $.
      $( [8-Aug-2011] $)
  $}


