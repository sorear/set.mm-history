$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Filters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.
    $( A filter containing a set ` A ` exists iff ` A ` has the finite
       intersection property (i.e. no finite intersection of elements of ` A `
       is empty).  Bourbaki TG I.37 prop. 1.  (Contributed by FL,
       20-Nov-2007.)  (The proof was shortened by Mario Carneiro,
       14-Dec-2013.) $)
    efilcp $p |- ( A e. V ->
                   ( -. (/) e. ( fi ` A ) <-> E. x e. Fil A C_ x ) ) $=
      ( wcel c0 cfi cfv wn wss cfil wrex wi c1o con0 wne mp2an sseq2 rcla4ev wa
      cvv wceq csn 1on 1n0 snfil 0ss sseq1 rexbidv mpbiri a1d a1i cfbas fsubbas
      cv fgfil adantl ssfii fbssfg sstr syl2an syl11anc sylbird exp3a pm2.61dne
      ex visset fiss mpan sseld filesn filfi eleq2d mtbird nsyli com12 rexlimiv
      cfg impbid1 ) BCDZEBFGZDZHZBAUNZIZAJKZVSWBWELZBEBEUAZWFLVSWGWEWBWGWEEWCIZ
      AJKZMUBZJDZEWJIZWIMNDMEOWKUCUDMNUEPWJUFWHWLAWJJWCWJEQRPWGWDWHAJBEWCUGUHUI
      UJUKVSBEOZWBWEVSWMWBSVTULDZWEBCUMVSWNWEVSWNSVTVQGZJDZBWOIZWEWNWPVSVTUOUPV
      SBVTIVTWOIWQWNBCUQVTURBVTWOUSUTWDWQAWOJWCWOBQRVAVEVBVCVDWDWBAJWDWCJDZWBWD
      WAEWCFGZDZWRWDVTWSEWCTDWDVTWSIAVFBWCTVGVHVIWRWTEWCDWCVJWRWSWCEWCVKVLVMVNV
      OVPVR $.
      $( [14-Dec-2013] $) $( [20-Nov-2007] $)
  $}

  ${
    $d A y $.  $d F x y $.
    $( A filter is closed under taking finite intersections.  (Contributed by
       FL, 27-Apr-2008.)  (The proof was shortened by Mario Carneiro,
       14-Dec-2013.) $)
    filint2 $p |- ( F e. Fil -> ( ( A C_ F /\ A =/= (/) /\ A e. Fin )
           -> |^| A e. F ) ) $=
      ( cfil wcel wss c0 wne cfn w3a cint cfi cfv elfir ex filfi eleq2d sylibd
      ) BCDZABEAFGAHDIZAJZBKLZDZTBDRSUBABCMNRUABTBOPQ $.
      $( [14-Dec-2013] $) $( [27-Apr-2008] $)
  $}


  ${
    $d A y z $.  $d B y $.  $d C y z $.
    fisub.1 $e |- B = { z | E. y ( y C_ A /\ y e. Fin /\ z = |^| y ) } $.
    fisub.2 $e |- D = { z | E. y ( y C_ C /\ y e. Fin /\ z = |^| y ) } $.
    $( If a set has the finite intersection property, its subsets have also
       this property.  (Contributed by FL, 27-Apr-2008.) $)
    fisub $p |- ( C C_ A -> ( -. (/) e. B -> -. (/) e. D ) ) $=
      ( wss c0 wcel cv wceq w3a wex 0ex 3anbi3d exbidv elab2 wi cint eqeq1 sstr
      cfn biimpri 19.23bi 3exp syl expcom com4l 3imp exlimiv com12 syl5bi con3d
      wa ) ECIZJFKZJDKZURALZEIZUTUDKZJUTUAZMZNZAOZUQUSVAVBBLZVCMZNZAOVFBJFPVGJM
      ZVIVEAVJVHVDVAVBVGJVCUBZQRHSVFUQUSVEUQUSTZAVAVBVDVLUQVAVBVDUSVAUQVBVDUSTT
      ZVAUQUPUTCIZVMUTECUCVNVBVDUSVNVBVDNZUSAUSVOAOZVNVBVHNZAOVPBJDPVJVQVOAVJVH
      VDVNVBVKQRGSUEUFUGUHUIUJUKULUMUNUO $.
      $( [27-Apr-2008] $)
  $}

  ${
    $d A x y $.  $d X x y $.
    $( Filter generated by a subbasis ` A ` .  Bourbaki TG I.37 paragraph above
       prop. 1.  (The theorem has been slightly modified because the
       definitions of the empty set are different in Bourbaki and Metamath.)
       (Contributed by FL, 27-Apr-2008.)  (The proof was shortened by Mario
       Carneiro, 1-Dec-2013.) $)
    fgsb2 $p |- ( ( A C_ ~P X /\ X e. _V /\ A =/= (/) ) ->
      ( -. (/) e. ( fi ` A ) ->
         { x e. ~P X | E. y e. ( fi ` A ) y C_ x } e. Fil ) ) $=
      ( cpw wss cvv wcel c0 wne w3a cfi cfv wn cv wrex wa cuni wceq syl crab wb
      cfil cfbas simpl3 simpr simpl1 simpl2 pwexg ssexg sylan2 syl11anc fsubbas
      jca mpbird fiuni sspwuni sylib eqsstr3d 3jca eqid jctir fgfild simpld ex
      ) CDEZFZDGHZCIJZKZICLMZHNZBOAOFBVKPAVFUAZUCHZVJVLQZVNDVMRSVOABVKVMDVOVKUD
      HZVKRZDFZVHKVMVMSVOVPVRVHVOVPVIVLQZVOVIVLVGVHVIVLUEVJVLUFUNVOCGHZVPVSUBVO
      VGVHVTVGVHVIVLUGZVGVHVIVLUHZVHVGVFGHVTDGUICVFGUJUKULZCGUMTUOVOVQCRZDVOVTW
      DVQSWCCGUPTVOVGWDDFWACDUQURUSWBUTVMVAVBVCVDVE $.
      $( [1-Dec-2013] $) $( [27-Apr-2008] $)
  $}

  ${
    $d A g $.  $d A x y $.  $d F g $.  $d F x y $.
    $( Condition to have a filter finer than a given filter and containing a
       set ` A ` .  Bourbaki T.G. I.37 cor. 1 (Contributed by FL,
       27-Apr-2008.)  (The proof was shortened by Mario Carneiro,
       14-Dec-2013.) $)
    cnfilca $p |- ( ( F e. Fil /\ A C_ U. F /\ A =/= (/) ) ->
    ( E. g e. Fil ( A e. g /\ F C_ g ) <-> A. x e. F ( x i^i A ) =/= (/) ) ) $=
      ( vy cfil wcel wss c0 wne cv wa wrex cin wral cvv wb ancoms syl cfbas w3a
      cuni csn cun cfi cfv uniexg ssexg sylan2 3adant3 snssg anbi1d unss syl6bb
      rexbidv snex simp1 unexg sylancr efilcp sylan snfil filfbas 3impa fbunfip
      3ad2ant1 syl11anc simp2 wsbc ralsng wceq ineq1 incom syl6eq neeq1d sbcieg
      wn ralbidv bitrd 3bitr2d ) DFGZBDUBZHZBIJZUAZBCKZGZDWFHZLZCFMBUCZDUDZWFHZ
      CFMZIWKUEUFGVQZAKZBNZIJZADOZWEWIWLCFWEBPGZWIWLQWAWCWSWDWCWAWSWAWCWBPGZWSD
      FUGZBWBPUHZUIRUJWSWIWJWFHZWHLWLWSWGXCWHBWFPUKULWJDWFUMUNSUOWEWKPGZWNWMQWE
      WJPGWAXDBUPWAWCWDUQWJDPFURUSCWKPUTSWEWNEKZWONZIJZADOZEWJOZWRWEWJTGZDTGZWN
      XIQWAWCWDXJWAWCLWSWDXJWAWTWCWSXAWCWTWSXBRVAWSWDLWJFGXJBPVBWJVCSVAVDWAWCXK
      WDDVCVFEAWJDVEVGWEWSXIWRQWEWCWTWSWAWCWDVHWAWCWTWDXAVFXBVGWSXIXHEBVIWRXHEB
      PVJXHWREBPXEBVKZXGWQADXLXFWPIXLXFBWONWPXEBWOVLBWOVMVNVOVRVPVSSVSVT $.
      $( [14-Dec-2013] $) $( [27-Apr-2008] $)
  $}

  ${
    $d w x y z A $.  $d w y z V $.  $d w x y z X $.
    $( Relative complements of the finite parts of an infinite set is a
       filter.  When ` A = NN ` the set of the relative complements is called
       Frechet's filter and is used to define the concept of limit of a
       sequence.  (Contributed by FL, 14-Jul-2008.) $)
    rcfpfil $p |- ( ( X e. V /\ A C_ X /\ -. A e. Fin ) ->
                    ( { x e. ~P X | ( A \ x ) e. Fin } e. Fil /\
                      X = U. { x e. ~P X | ( A \ x ) e. Fin } ) ) $=
      ( vy vz vw wcel wss cfn cv cdif wceq wa difeq2 eleq1d visset wsbc sbcie
      c0 wn w3a cpw crab cfil cuni elrab elpw anbi1i bitri a1i elisset 3ad2ant1
      cvv ssdif0 0fin eleq1 mpbiri sylbi sbcieg biimpar sylan2 3adant3 0ex dif0
      wb eleq1i biimpi con3i 3ad2ant3 sscon ssfi expcom syl 3imtr4g cin difindi
      wi cun unfi syl5eqel anbi12i inex1 isfild isfildu jca ) DCHZBDIZBJHZUAZUB
      ZBAKZLZJHZADUCZUDZUEHDWPUFMWKBEKZLZJHZEFGDWPWQWPHZWQDIZWSNZVFWKWTWQWOHZWS
      NXBWNWSAWQWOWLWQMWMWRJWLWQBOPUGXCXAWSWQDEQUHUIUJUKZWGWHDUNHWJDCULUMZWGWHW
      SEDRZWJWHWGBDLZJHZXFWHXGTMZXHBDUOXIXHTJHUPXGTJUQURUSWGXFXHWSXHEDCWQDMWRXG
      JWQDBOPUTVAVBVCZWJWGWSETRZUAWHXKWIXKWIXKBTLZJHZWIWSXMETVDWQTMWRXLJWQTBOPS
      XLBJBVEVGUJVHVIVJGKZFKZIZWKWSEXNRZWSEXORZVRXODIZXPBXNLZJHZBXOLZJHZXQXRXPY
      BXTIZYAYCVRXNXOBVKYAYDYCXTYBVLVMVNWSYAEXNGQWQXNMWRXTJWQXNBOPSZWSYCEXOFQZW
      QXOMWRYBJWQXOBOPSZVOVJWKXSXNDIUBZYCYANZBXOXNVPZLZJHZXRXQNWSEYJRYIYLVRYHYI
      YKYBXTVSJBXOXNVQYBXTVTWAUKXRYCXQYAYGYEWBWSYLEYJXOXNYFWCWQYJMWRYKJWQYJBOPS
      VOWDWKWSEDWPXDXEXJWEWF $.
      $( [1-Dec-2013] $) $( [14-Jul-2008] $)
  $}

  ${
    $d y A $.  $d x y B $.
    $( Lemma for ~ csdfil .  (Requires AC via ~ infunabs .) $)
    csdfillem $p |- ( ( om ~<_ X /\ A ~<_ B /\ B ~< X ) -> ( A u. B ) ~< X ) $=
      ( vx vy com cdom wbr csdm cun cvv wcel wa 3ad2ant3 wi cv wceq expcom syld
      cen w3a wo omex sdomex simpld entri2 reldom brrelexi 3ad2ant2 simp2 breq2
      sylancr anbi12d uneq1 id breq12d imbi12d breq1 anbi2d uneq2 syl6eq breq1d
      uncom infunabs vtocl2g exp3acom23 imp syl21anc ensdomtr domsdomtr syl5com
      visset unfi2 simprd simp1 sdomdomtr sylc jaod mpd ) FCGHZABGHZBCIHZUAZFBG
      HZBFIHZUBZABJZCIHZWCFKLBKLZWFUCWCWICKLZWBVTWIWJMWABCUDNZUEZFBKKUFULWCWDWH
      WEWCWDWGBTHZWHWCWIAKLZWAWDWMOZWLWAVTWNWBABGUGUHUIVTWAWBUJZWIWNMZWAWOWQWDW
      AWMFDPZGHZEPZWRGHZMZWRWTJZWRTHZOWDWTBGHZMZBWTJZBTHZOWDWAMZWMODEBAKKWRBQZX
      BXFXDXHXJWSWDXAXEWRBFGUKWRBWTGUKUMXJXCXGWRBTWRBWTUNXJUOUPUQWTAQZXFXIXHWMX
      KXEWAWDWTABGURUSXKXGWGBTXKXGBAJWGWTABUTBAVCVAVBUQWRWTDVLEVLVDVEVFVGVHWBVT
      WMWHOWAWMWBWHWGBCVIRNSWCWEWGFIHZWHWCWAWEXLWPWEWAAFIHZXLWAWEXMABFVJRXMWEXL
      ABVMRSVKWCWJVTXLWHOWCWIWJWKVNVTWAWBVOWJXLVTWHWGFCKVPVFVQSVRVS $.
      $( [14-Dec-2013] $)
  $}

  ${
    $d w x y z X $.  $d w y z V $.
    $( The set of all elements whose complement is dominated by the base set is
       a filter.  (Contributed by Mario Carneiro, 14-Dec-2013.) $)
    csdfil $p |- ( ( X e. V /\ om ~<_ X ) ->
                   ( { x e. ~P X | ( X \ x ) ~< X } e. Fil /\
                     X = U. { x e. ~P X | ( X \ x ) ~< X } ) ) $=
      ( vy wcel com cdom wbr wa cv cdif csdm wceq wss wb difeq2 breq1d cvv wsbc
      c0 vz vw cpw crab cfil cuni elrab visset elpw anbi1i bitri elisset adantr
      a1i difid wne cfn 0fin isfinite3 domnsym mt2 breq2 mtbiri necon2ai adantl
      mpbi 0sdomg syl mpbird syl5eqbr sbcieg wn sdomirr 0ex syl6eq sbcie mtbird
      dif0 w3a wi simp3 simp1l difexg sscon ssdomg syl5com domsdomtr ex 3imtr4g
      syl6 sylc cin wo entri3 syl11anc simpr csdfillem syl3an3 3exp simpl uncom
      cun jaod mpan9 difindi breq1i syl6ibr 3ad2ant1 anbi12i isfild isfildu jca
      inex1 ) CBEZFCGHZIZCAJZKZCLHZACUCZUDZUEECYAUFMXPCDJZKZCLHZDUAUBCYAYBYAEZY
      BCNZYDIZOXPYEYBXTEZYDIYGXSYDAYBXTXQYBMXRYCCLXQYBCPQUGYHYFYDYBCDUHUIUJUKUN
      ZXNCREZXOCBULUMZXPYDDCSZCCKZCLHZXPYMTCLCUOXPTCLHZCTUPZXOYPXNXOCTCTMXOFTGH
      ZYQTFLHZTUQEYRURTUSVFFTUTVACTFGVBVCVDVEXPYJYOYPOYKCRVGVHVIVJXPYJYLYNOYKYD
      YNDCRYBCMYCYMCLYBCCPQVKVHVIZXPYDDTSZCCLHZUUAVLXPCVMUNYTUUAOXPYDUUADTVNYBT
      MZYCCCLUUBYCCTKCYBTCPCVRVOQVPUNVQXPUAJZCNZUBJZUUCNZVSZCUUEKZCLHZCUUCKZCLH
      ZYDDUUESZYDDUUCSZUUGUUFUUJREZUUIUUKVTZXPUUDUUFWAUUGXNUUNXNXOUUDUUFWBCUUCB
      WCZVHUUFUUNUUJUUHGHZUUOUUFUUJUUHNUUNUUQUUEUUCCWDUUJUUHRWEWFUUQUUIUUKUUJUU
      HCWGWHWJWKYDUUIDUUEUBUHYBUUEMYCUUHCLYBUUECPQVPZYDUUKDUUCUAUHZYBUUCMYCUUJC
      LYBUUCCPQVPZWIXPUUDUUECNZVSUUKUUIIZCUUCUUEWLZKZCLHZUUMUULIYDDUVCSXPUUDUVB
      UVEVTUVAXPUVBUUJUUHXBZCLHZUVEXNUUQUUHUUJGHZWMZXOUVBUVGVTZXNUUNUUHREUVIUUP
      CUUEBWCUUJUUHRRWNWOXOUUQUVJUVHXOUUQUVBUVGUVBXOUUQUUIUVGUUKUUIWPUUJUUHCWQW
      RWSXOUVHUVBUVGUVBXOUVHUUKUVGUUKUUIWTXOUVHUUKVSUVFUUHUUJXBCLUUJUUHXAUUHUUJ
      CWQVJWRWSXCXDUVDUVFCLCUUCUUEXEXFXGXHUUMUUKUULUUIUUTUURXIYDUVEDUVCUUCUUEUU
      SXMYBUVCMYCUVDCLYBUVCCPQVPWIXJXPYDDCYAYIYKYSXKXL $.
      $( [14-Dec-2013] $)
  $}

  $( An element of a filter can't be empty.  (Contributed by FL,
     24-May-2011.) $)
  elfilnemp $p |- ( ( F e. Fil /\ A e. F ) -> A =/= (/) ) $=
    ( cfil wcel c0 wn wne filesn wa wceq nelneq df-ne sylibr ex mpan9 ) BCDEBDF
    ZABDZAEGZBHQPRQPIAEJFRAEBKAELMNO $.
    $( [24-May-2011] $)


  ${
    $d A n o $.  $d B n o x $.  $d F o $.  $d J n o $.  $d N n o $.
    $d X n o $.  $d Y n o $.
    fbaslim2.1 $e |- X = U. J $.
    fbaslim2.2 $e |- Y = U. B $.
    fbaslim2.3 $e |- F = ( filGen ` B ) $.
    fbaslim2.4 $e |- N = ( ( nei ` J ) ` { A } ) $.
    $( A condition for a filter base ` B ` to converge to a point ` A ` .  Use
       neighborhoods instead of open neighborhoods.  Compare ~ fbaslim .
       (Contributed by FL, 4-Jul-2011.) $)
    fbaslim2 $p |- ( ( ( J e. Top /\ B e. fBas /\ X = Y ) /\ A e. X ) -> ( A e.
    ( ( fLim ` J ) ` F ) <-> A. n e. N E. x e. B x C_ n ) ) $=
      ( vo wcel wa cfv wrex wi com12 ctop cfbas wceq w3a cflim wss wral fbaslim
      cv cnei eleq2 isneip r19.29r pm2.27 r19.41v sstr reximi sylbir syl6 com23
      csn ex imp31 rexlimivw syl expcom adantl 3ad2antl1 anabsi5 ax-mp ralrimiv
      syl6bi wal df-ral weq eleq1 rexbidv imbi12d cbvalv opnneip imbi1d syl5com
      sseq2 biimpd 3exp com34 3ad2ant1 adantr alimdv syl6ibr sylbi impbid bitrd
      ) FUAOZCUBOZHIUCZUDZBHOZPZBEFUEQQOBNUIZOZAUIZWTUFZACRZSZNFUGZXBDUIZUFZACR
      ZDGUGZABCNEFHIJKLUHWSXFXJWSXFXJWSXFPZXIDGXGGOZXKXIGBVAFUJQQZUCZXLXKXISZSM
      XNXLXGXMOZXOGXMXGUKXKXPXIWSXFXPXISWSXPXKXIWNWOWRXPXOSWPWNWRPXPXGHUFZXAWTX
      GUFZPZNFRZPXOBNFXGHJULXTXOXQXKXTXIXFXTXISWSXTXFXIXTXFPXSXEPZNFRXIXSXENFUM
      YAXINFXAXRXEXIXAXEXRXIXAXEXDXRXISXAXDUNXDXRXIXDXRPXCXRPZACRXIXCXRACUOYBXH
      ACXBWTXGUPUQURVBUSUTVCVDVEVFVGTVGVLVHUTVITVLVJTVKVBXJWSXFXJXLXISZDVMZWSXF
      SZXIDGVNYDWTGOZXDSZNVMZYEYCYGDNDNVOZXLYFXIXDXGWTGVPYIXHXCACXGWTXBWCVQVRVS
      WSYHXFWSYHWTFOZXESZNVMXFWSYGYKNWQYGYKSZWRWNWOYLWPWNYJYGXEWNYJXAYGXDWNYJXA
      YGXDSWNYJXAUDWTXMOZYGXDBFWTVTXNYGYMXDSZSMXNYGYNXNYFYMXDGXMWTUKWAWDVJWBWEW
      FUTWGWHWIXENFVNWJTWKWKTWLWM $.
      $( [8-Aug-2011] $) $( [4-Jul-2011] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d X x y $.  $d Y x y $.
    fbfgss2.1 $e |- X = U. F $.
    fbfgss2.2 $e |- Y = U. G $.
    $( A condition for a filter to be finer than another filter.  Compare
       ~ fbfgss .  (Contributed by FL, 8-Aug-2011.) $)
    fbfgss2 $p |- ( ( F e. Fil /\ G e. Fil /\ X = Y ) ->
      ( F C_ G <-> A. x e. F E. y e. G y C_ x ) ) $=
      ( cfbas wcel wceq w3a cfil wss cv wb filfbas cfg cfv fgid fbfgss 3ad2ant1
      wrex wral id 3anim123i 3ad2ant2 sseq12d bibi1d syl5ibcom mpcom ) CIJZDIJZ
      EFKZLZCMJZDMJZUNLZCDNZBOAONBDUCACUDZPZUPULUQUMUNUNCQDQUNUEUFUOCRSZDRSZNZU
      TPURVAABCDEFGHUAURVDUSUTURVBCVCDUPUQVBCKUNCTUBUQUPVCDKUNDTUGUHUIUJUK $.
      $( [8-Aug-2011] $)
  $}

  ${
    $d u v x y z A $.  $d u v x y z L $.  $d u v x y z Y $.
    trfil.1 $e |- Y = U. L $.
    $( Conditions for the trace of a filter ` L ` to be a filter.  (Contributed
       by FL, 2-Sep-2013.) $)
    trfil $p |- ( ( A C_ Y /\ L e. Fil /\ A. v e. L ( v i^i A ) =/= (/) ) ->
                  ( { u | E. v e. L u = ( v i^i A ) } e. Fil /\
                    A = U. { u | E. v e. L u = ( v i^i A ) } ) ) $=
      ( vx wss wcel cv cin c0 w3a wceq wrex wa eqeq1 rexbidv wsbc ineq1 vz cfil
      vy wne wral cab cuni wb visset elab inss2 sseq1 mpbiri rexlimivw pm4.71ri
      bitri a1i filusb ssexg sylan2 3adant3 adantl simpl sseqin2 eqcomd rcla4ev
      cvv sylib eqeq2d syl11anc sbcieg syl mpbird simp3 ralnex necom ralbii 0ex
      wn df-ne sbcie notbii 3bitr4i 3simpa cun simpl1r simprl elssuni syl6sseqr
      wi ad2antrl simpl2 simpl1l unssd ssun1 fillsb imp syl13anc simpl3 ssequn1
      sstr simprr uneq12d eqtr3d indir syl6eqr rexlimdv syl3an1 cbvrexv 3imtr4g
      dfss exp32 simp12 filint 3expa sylancl ineq2 inindir syl5ibrcom rexlimdva
      eqid eqeq1d imbi2d imp3a anbi12i inex1 isfild isfildu jca ) CEHZDUBIZAJZC
      KZLUDZADUEZMZBJZYMNZADOZBUFZUBICYTUGNYPGJZYMNZADOZGUAUCCYTUUAYTIZUUACHZUU
      CPZUHYPUUDUUCUUFYSUUCBUUAGUIYQUUANYRUUBADYQUUAYMQRUJUUCUUEUUBUUEADUUBUUEY
      MCHYLCUKUUAYMCULUMUNUOUPUQZYJYKCVGIZYOYKYJEDIZUUHDEFURZCEDUSUTZVAZYJYKUUC
      GCSZYOYJYKPZUUMCYMNZADOZUUNUUICECKZNZUUPYKUUIYJUUJVBUUNUUQCUUNYJUUQCNYJYK
      VCCEVDVHVEUUOUURAEDYLENYMUUQCYLECTVIVFVJUUNUUHUUMUUPUHUUKUUCUUPGCVGUUACNU
      UBUUOADUUACYMQRVKVLVMVAZYPYOUUCGLSZVSZYJYKYOVNLYMNZVSZADUEUVBADOZVSYOUVAU
      VBADVOYNUVCADYNLYMUDUVCYMLVPLYMVTUPVQUUTUVDUUCUVDGLVRUUALNUUBUVBADUUALYMQ
      RWAWBWCVHYPUAJZCHZUCJZUVEHZMUVGYQCKZNZBDOZUVEYMNZADOZUUCGUVGSZUUCGUVESZYP
      UUNUVFUVHUVKUVMWJYJYKYOWDUUNUVFUVHMZUVJUVMBDUVPYQDIZUVJUVMUVPUVQUVJPZPZYQ
      UVEWEZDIZUVEUVTCKZNZUVMUVSYKUVQUVTEHZYQUVTHZUWAYJYKUVFUVHUVRWFUVPUVQUVJWG
      UVSYQUVEEUVQYQEHUVPUVJUVQYQDUGEYQDWHFWIWKUVSUVFYJUVEEHUUNUVFUVHUVRWLZYJYK
      UVFUVHUVRWMUVECEXAVJWNUWEUVSYQUVEWOUQYKUVQUWDUWEMUWAYQUVTDEFWPWQWRUVSUVEU
      VIUVECKZWEZUWBUVSUVGUVEWEZUVEUWHUVSUVHUWIUVENUUNUVFUVHUVRWSUVGUVEWTVHUVSU
      VGUVIUVEUWGUVPUVQUVJXBUVSUVFUVEUWGNUWFUVECXKVHXCXDYQUVECXEXFUVLUWCAUVTDYL
      UVTNYMUWBUVEYLUVTCTVIVFVJXLXGXHUVNUVGYMNZADOZUVKUUCUWKGUVGUCUIUUAUVGNUUBU
      WJADUUAUVGYMQRWAUWJUVJABDYLYQNYMUVIUVGYLYQCTVIXIUPZUUCUVMGUVEUAUIZUUAUVEN
      UUBUVLADUUAUVEYMQRWAZXJYPUVFUVGCHZMYKUVOUVNPZUUCGUVEUVGKZSZWJYJYKYOUVFUWO
      XMYKUVMUVKPUWQUUACKZNZGDOZUWPUWRYKUVMUVKUXAYKUVLUVKUXAWJZADYKYLDIZPZUXBUV
      LUVKYMUVGKZUWSNZGDOZWJUXDUVJUXGBDUXDUVQPZUXGUVJYLYQKZCKZUWSNZGDOZUXHUXIDI
      ZUXJUXJNZUXLYKUXCUVQUXMYLYQDXNXOUXJYAUXKUXNGUXIDUUAUXINUWSUXJUXJUUAUXICTV
      IVFXPUVJUXFUXKGDUVJUXEUXJUWSUVJUXEYMUVIKUXJUVGUVIYMXQYLYQCXRXFYBRXSXTUVLU
      XAUXGUVKUVLUWTUXFGDUVLUWQUXEUWSUVEYMUVGTYBRYCXSXTYDUVOUVMUVNUVKUWNUWLYEUW
      RUWQYMNZADOZUXAUUCUXPGUWQUVEUVGUWMYFUUAUWQNUUBUXOADUUAUWQYMQRWAUXOUWTAGDY
      LUUANYMUWSUWQYLUUACTVIXIUPXJVLYGYPUUCGCYTUUGUULUUSYHYI $.
      $( [8-Dec-2013] $) $( [2-Sep-2013] $)
  $}

  ${
    $d v u A $.  $d v u P $.  $d v u J $.  $d v Y $.
    trnei.1 $e |- Y = U. J $.
    $( The trace, over a set ` A ` , of the filter of the neighborhoods of a
       point ` P ` is a filter iff ` P ` belongs to the closure of ` A ` .
       (This is ~ trfil applied to a filter of neighborhoods.)  (Contributed by
       FL, 15-Sep-2013.) $)
    trnei $p |- ( ( A C_ Y /\ J e. Top )
       -> ( P e. ( ( cls ` J ) ` A ) -> { u | E. v e. ( ( nei ` J ) ` { P } )
             u = ( v i^i A ) } e. Fil ) ) $=
      ( wss wcel wa cfv w3a cv wceq cfil imp jca c0 wne cuni ctop ccl cnei wrex
      cin csn simpll simplr clsss3 ancoms sseld 3jca simpr ex wral wb neindisj2
      cab 3com12 wi simp2 snssi 3ad2ant3 unnei simp1 sseq2 syl5ibr 3syl pm2.43i
      snnzg neifil syl111anc eqid trfil simpld 3expia syl11anc sylbid syl6 ) CF
      HZEUAIZJZDCEUBKKZIZVTWADFIZLZWDJZBMAMCUEZNADUFZEUCKKZUDBURZOIZWBWDWGWBWDJ
      ZWFWDWMVTWAWEVTWAWDUGVTWAWDUHWBWDWEWBWCFDWAVTWCFHCEFGUIUJUKPULWBWDUMQUNWF
      WDWLWFWDWHRSAWJUOZWLWAVTWEWDWNUPDCAEFGUQUSWFCWJTZHZWJOIZWNWLUTWFWPWFWAWIF
      HZJWOFNZWFWPUTWFWAWRVTWAWEVAZWEVTWRWADFVBVCZQWIEFGVDWFWPWSVTVTWAWEVEWOFCV
      FVGVHVIWFWAWRWIRSZWQWTXAWEVTXBWADFVJVCWIEFGVKVLWPWQWNWLWPWQWNLWLCWKTNABCW
      JWOWOVMVNVOVPVQVRPVS $.
      $( [10-Nov-2013] $) $( [15-Sep-2013] $)
  $}


