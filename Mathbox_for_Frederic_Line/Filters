$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Filters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Fil $. $( The class of all filters. $)

  $( Extend class notation with the class of all filters. $)
  cfil $a class Fil $.

  ${
    $d f x y $.
    $( The class of all filters.  Bourbaki TG I.36 def. 1 axioms FI, FIIa,
       FIIb, FIII.  Filters are used to define the concept of limit in the
       general case.  It's a generalization of the idea of neighborhoods.  The
       problem with the concept of neighborhoods is that ( in ` RR ` for
       instance ) you can't express the idea of a variable which tends to
       infinity.  A workaround for this limitation is to use the idea of limit
       point.  However limit points introduce extra sets which are
       fundamentally unneeded.  A better idea is to use filter which succeeds
       in solving the problem by remaining in the studied set.  Filters were
       invented by Cartan in 1937 and made famous by Bourbaki in his treatise.
       A notion similar to the notion of filter is the concept of net invented
       by Moore and Smith in 1922. $)
    df-fil $a |- Fil = { f | ( ( -. (/) e. f /\ U. f e. f ) /\
      A. x A. y ( ( x e. f /\ y (_ U. f  /\ x (_ y ) -> y e. f ) /\
      A. x e. f A. y e. f ( x i^i y ) e. f ) } $.
  $}

  ${ $d F f x y $.  $d X f $.
    isfil.1 $e |- X = U. F $.
    $( The predicate "is a filter." $)
    isfil $p |- ( F e. A -> ( F e. Fil <-> ( ( -. (/) e. F /\ X e. F ) /\
    A. x A. y ( ( x e. F /\ y (_ X /\ x (_ y ) -> y e. F ) /\
    A. x e. F A. y e. F ( x i^i y ) e. F ) ) ) $=
      ( vf c0 cv wcel wn cuni wa wss w3a wi wal cin wral cfil wceq eleq2
      notbid unieq syl6eqr id eleq12d anbi12d sseq2d 3anbi12d imbi12d 2albidv
      raleqd 3anbi123d df-fil elab2g ) HGIZJZKZUQLZUQJZMZAIZUQJZBIZUTNZVCVENZOZ
      VEUQJZPZBQAQZVCVERZUQJZBUQSZAUQSZOHDJZKZEDJZMZVCDJZVEENZVGOZVEDJZPZBQAQZV
      LDJZBDSZADSZOGDTCUQDUAZVBVSVKWEVOWHWIUSVQVAVRWIURVPUQDHUBUCWIUTEUQDWIUTDL
      EUQDUDFUEZWIUFUGUHWIVJWDABWIVHWBVIWCWIVDVTVFWAVGUQDVCUBWIUTEVEWJUIUJUQDVE
      UBUKULVNWGAUQDVMWFBUQDUQDVLUBUMUMUNABGUOUP $.
      $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  ${
    $d F x y $.
    $( The empty set doesn't belong to a filter. $)
    filesn $p |- ( F e. Fil -> -. (/) e. F ) $=
      ( vx vy cfil wcel c0 wn cuni wa cv wss w3a wi wal cin wral eqid isfil
      ibi 3simp1d pm3.26d ) ADEZFAEGZAHZAEZUBUCUEIZBJZAECJZUDKUGUHKLUHAEMCNBNZU
      GUHOAECAPBAPZUBUFUIUJLBCDAUDUDQRSTUA $.
      $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  ${ $d A x y $.  $d B y $.  $d F x y $.  $d X x y $.
    fillsb.1 $e |- X = U. F $.
    $( A filter is closed under taking supersets. $)
    fillsb $p |- ( F e. Fil -> ( ( A e. F /\
      B (_ X /\ A (_ B ) -> B e. F ) ) $=
      ( vx vy cfil wcel wss w3a cvv wi cuni uniexg syl5eqel cpw wa elpw2g
      bicomd anbi2d 3simpa syl5bi cv wceq eleq1 sseq1 3anbi13d imbi1d imbi2d
      sseq2 3anbi23d imbi12d c0 wn wal cin wral isfil ibi 3simp2d 19.21bbi
      vtocl2g syl6 com23 mpcom pm2.43d ) CHIZACIZBDJZABJZKZBCIZDLIZVHVLVLVMMZMV
      HCNLDCHOEPVNVLVHVOVNVLVIBDQZIZRZVHVOMZVNVIVJRVRVLVNVJVQVIVNVQVJBDLSTUAVIV
      JVKUBUCVHFUDZCIZGUDZDJZVTWBJZKZWBCIZMZMVHVIWCAWBJZKZWFMZMVSFGABCVPVTAUEZW
      GWJVHWKWEWIWFWKWAVIWDWHWCVTACUFVTAWBUGUHUIUJWBBUEZWJVOVHWLWIVLWFVMWLWCVJW
      HVKVIWBBDUGWBBAUKULWBBCUFUMUJVHWGFGVHUNCIUODCIRZWGGUPFUPZVTWBUQCIGCURFCUR
      ZVHWMWNWOKFGHCDEUSUTVAVBVCVDVEVFVG $.
      $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  ${ $d F x y $.
    filusb.1 $e |- X = U. F $.
    $( The underlying set belongs to the filter. $)
    filusb $p |- ( F e. Fil -> X e. F ) $=
      ( vx vy cfil wcel c0 wn wa cv wss w3a wi wal cin wral isfil ibi 3simp1d
      pm3.27d ) AFGZHAGIZBAGZUBUCUDJZDKZAGEKZBLUFUGLMUGAGNEODOZUFUGPAGEAQDAQZUB
      UEUHUIMDEFABCRSTUA $.
      $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  ${ $d A x y $.  $d B y $.  $d F x y $.
  $( A filter is closed under taking intersections. $)
  filint $p |- ( ( F e. Fil /\ A e. F /\ B e. F ) -> ( A i^i B ) e. F ) $=
    ( vx vy cfil wcel cin wa wi cv wral c0 wn cuni wss w3a wal eqid isfil
    biimpa 3simp3d ex wceq ineq1 eleq1d ineq2 rcla42v com12 syl6 pm2.43i
    3impib ) CFGZACGZBCGZABHZCGZUMUNUOIZUQJZUMUMDKZEKZHZCGZECLDCLZUSUMUMVDUMUMI
    MCGNCOZCGIZUTCGVAVEPUTVAPQVACGJERDRZVDUMUMVFVGVDQDEFCVEVESTUAUBUCURVDUQVCUQ
    AVAHZCGDEABCCUTAUDVBVHCUTAVAUEUFVABUDVHUPCVABAUGUFUHUIUJUKUL $.
    $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  $( The intersection of two elements of a filter can't be empty. $)
  fipfil $p |- ( F e. Fil -> ( ( A e. F /\ B e. F ) ->
    ( A i^i B ) =/= (/) ) ) $=
    ( cfil wcel wa cin c0 wceq wn wne nelneq filint 3expb filesn adantr sylanc
    ex df-ne syl6ibr ) CDEZACEZBCEZFZABGZHIJZUEHKUAUDUFUECEZHCEJZUFUAUDFUEHCLUA
    UBUCUGABCMNUAUHUDCOPQRUEHST $.
    $( [19-Sep-2007] $) $( [16-Sep-2007] $)

  ${
    $d A y $.  $d F x y $.
    $( A filter has the finite intersection property.  Bourbaki TG I.36 note
       of def. 1. $)
    fipfil2 $p |- ( F e. Fil
         -> ( ( A (_ F /\ A =/= (/) /\ A e. Fin ) -> |^| A =/= (/) ) ) $=
      ( vy vx cfil wcel wss c0 wne cfn w3a cint wceq wn wi wa cvv ssexg cv wal
      sseq1 neeq1 eleq1 3anbi123d wb inteq eqeq1 notbid syl imbi12d cla4gv
      nelneq cin wral cuni eqid isfil biimpa 3simp3d anidms fiint sylib
      19.21bi imp filesn adantr sylanc ex 19.21aiv syl5 com23 com14 pm2.43i
      com13 3ad2ant1 com12 df-ne syl6ibr ) BEFZABGZAHIZAJFZKZALZHMZNZWDHIWCVSWF
      WCVSWFOZVTWAWCWGOZWBVSWCVTWFVSWCVTWFOOVTVSWCVSWFVTVSWHVTVSPAQFZWHABERWIVS
      WCWFWICSZBGZWJHIZWJJFZKZWJLZHMZNZOZCTWCWFOZVSWRWSCAQWJAMZWNWCWQWFWTWKVTWL
      WAWMWBWJABUAWJAHUBWJAJUCUDWTWOWDMZWQWFUEWJAUFXAWPWEWOWDHUGUHUIUJUKVSWRCVS
      WNWQWOBFZHBFNZWQVSWNPWOHBULVSWNXBVSWNXBOZCVSWJDSZUMBFDBUNCBUNZXDCTVSXFVSV
      SPXCBUOZBFPZWJBFXEXGGWJXEGKXEBFODTCTZXFVSVSXHXIXFKCDEBXGXGUPUQURUSUTCDBVA
      VBVCVDVSXCWNBVEVFVGVHVIVJVKUIVHVLVMVNVOVMVPWDHVQVR $.
      $( [2-Sep-2008] $) $( [20-Sep-2007] $)
  $}

  $( The empty set is not a filter.  Bourbaki TG I.36 def 1 note. $)
  emnfil $p |- -. (/) e. Fil $=
    ( c0 cfil wcel cuni noel eqid filusb mto ) ABCADZACIEAIIFGH $.
    $( [31-Oct-2007] $) $( [30-Oct-2007] $)

  ${ $d A x y $.  $d B x y $.
    $( A singleton is a filter.  Bourbaki TG I.36, example 1. $)
    oefil2 $p |- ( ( A e. B /\ A =/= (/) ) -> { A } e. Fil ) $=
      ( vx vy wcel c0 wne wa csn wn cuni cv wss w3a wi wal cin wral cfil elsni
      eqcomd necon3ai adantl wceq unisng adantr snidg eqeltrd jca sseq2d sseq1
      eqss biimpri ancoms elsn sylibr ex syl6bi com3r com23 syl5ib 3impd
      19.21aivv ineq12 inidm syl6eq visset inex1 elsnc syl2anb rgen2a a1i 3jca
      cvv wb snex eqid isfil ax-mp ) ABEZAFGZHZFAIZEZJZWCKZWCEZHZCLZWCEZDLZWFMZ
      WIWKMZNWKWCEZOZDPCPZWIWKQZWCEZDWCRCWCRZNZWCSEZWBWHWPWSWBWEWGWAWEVTWDAFWDF
      AFATUAUBUCWBWFAWCVTWFAUDWAABUEZUFVTAWCEWAABUGUFUHUIVTWPWAVTWOCDVTWJWLWMWN
      VTWIAUDZWLWMWNOZOWJVTWLXCXDVTWLWKAMZXCXDOVTWFAWKXBUJXCWMXEWNXCWMAWKMZXEWN
      OWIAWKUKXFXEWNXFXEHWKAUDZWNXEXFXGXGXEXFHWKAULUMUNDAUOZUPUQURUSURUTCAUOZVA
      VBVCUFWSWBWRCDWCXCXGWRWJWNXCXGHZWQAUDWRXJWQAAQAWIAWKAVDAVEVFWQAWIWKCVGVHV
      IUPXIXHVJVKVLVMWCVNEXAWTVOAVPCDVNWCWFWFVQVRVSUP $.
      $( [19-Sep-2007] $) $( [16-Sep-2007] $)
  $}

  ${ $d J x y $.  $d S x y $.  $d X x y $.
    neifil.1 $e |- X = U. J $.
    $( The neighborhoods of a non empty set is a filter.
       Bourbaki TG I.36, example 2. $)
    neifil $p |- ( ( J e. Top /\ S (_ X /\ S =/= (/) ) ->
      ( ( nei ` J ) ` S ) e. Fil ) $=
      ( vx vy ctop wcel wss c0 wne w3a cnei cfv wn cuni wa cv wi wal cin wral
      cfil 0nnei 3adant2 unnei tpnei biimpa eqeltrd 3adant3 jca wceq sseq2
      ssnei2 exp43 adantr com14 syl6bi com23 3impd mpcom 19.21aivv innei
      3expib 3ad2ant1 r19.21aivv 3jca cvv wb fvex eqid isfil ax-mp sylibr ) BGH
      ZACIZAJKZLZJABMNZNZHOZVTPZVTHZQZERZVTHZFRZWBIZWEWGIZLZWGVTHZSZFTETZWEWGUA
      VTHZFVTUBEVTUBZLZVTUCHZVRWDWMWOVRWAWCVOVQWAVPABUDUEVOVPWCVQVOVPQZWBCVTABC
      DUFZVOVPCVTHABCDUGUHUIUJUKVRWLEFVOVPWLVQWBCULZWRWLWSWTWJWRWKWTWFWHWIWRWKS
      ZWTWHWFWIXASZWTWHWGCIZWFXBSWBCWGUMWRWFWIXCWKVOWFWIXCWKSSSVPVOWFWIXCWKABWG
      WECDUNUOUPUQURUSUTUSVAUJVBVRWNEFVTVTVOVPWFWKQWNSVQVOWFWKWNABWGWEVCVDVEVFV
      GVTVHHWQWPVIAVSVJEFVHVTWBWBVKVLVMVN $.
      $( [19-Sep-2007] $) $( [18-Sep-2007] $)
  $}

  ${ $d F x y $.  $d G x y $.
  $( The intersection of two filters is a filter.  Use  ~ fiint to extend this
     property to the intersection of a finite set of filters.  Bourbaki TG I.36
     par. 3. $)
  filintf $p |- ( ( F e. Fil /\ G e. Fil /\ U. F = U. G ) ->
      ( F i^i G ) e. Fil ) $=
    ( vx vy cfil wcel cuni wceq w3a cin wi cvv c0 wn wa cv wss wal wral eqid
    isfil filesn elin pm3.26bi nsyl 3ad2ant1 ineq2 inidm syl5eqr 3ad2ant3
    filusb eleq1 biimpri expcom syl6bir com3l syl syl5com 3imp eqeltrrd
    elssuni uniin jctil eqss sylibr eqeltrd jca fillsb 3ad2ant2 anim12d biimpi
    sstr2 mpi ssin pm4.24 3anim123i an6 syl5 syl6ibr 19.21aivv filint 3expib
    im2anan9 com12 an4s syl2anb 3adant3 r19.21aivv 3jca syl5cbir inex1g
    pm2.43i ) AEFZBEFZAGZBGZHZIZABJZEFZWMWNWRWTKWQWRWSLFZWTWMXAWTMWSFZNZWSGZWSF
    ZOZCPZWSFZDPZXDQZXGXIQZIZXIWSFZKZDRCRZXGXIJZWSFZDWSSCWSSZIWRCDLWSXDXDTUAWRX
    FXOXRWRXCXEWMWNXCWQWMMAFZXBAUBXBXSMBFMABUCUDUEUFWRXDWOWPJZWSWRXDXTQZXTXDQZO
    XDXTHWRYBYAWRXTWSFYBWRWOXTWSWQWMWOXTHWNWQWOWOJXTWOWOWPWOUGWOUHUIUJWMWNWQWOW
    SFZWNWOAFZWQYCKZWMWNWPBFZYDYEKBWPWPTZUKWQYFYDYCWQYFWOBFZYDYCKWOWPBULYDYHYCY
    CYDYHOWOABUCUMUNUOUPUQAWOWOTZUKURUSUTZXTWSVAUQABVBZVCXDXTVDVEYJVFVGWRXNCDWR
    XLXIAFZXIBFZOZXMWRXGAFZXIWOQZXKIZXGBFZXIWPQZXKIZOZYNXLWRYQYLYTYMWMWNYQYLKWQ
    XGXIAWOYIVHUFWNWMYTYMKWQXGXIBWPYGVHVIVJXLYOYROZYPYSOZXKXKOZIUUAXHUUBXJUUCXK
    UUDXHUUBXGABUCZVKXJXIXTQZUUCXJYAUUFYKXIXDXTVLVMXIWOWPVNVEXKUUDXKVOVKVPYOYPX
    KYRYSXKVQVEVRXIABUCZVSVTWRXQCDWSWSWMWNXHXMOZXQKWQUUHWMWNOZXQUUBYNUUIXQKZXHX
    MYOYLYRYMUUJUUIYOYLOZYRYMOZOZXQUUIUUMXPAFZXPBFZOXQWMUUKUUNWNUULUUOWMYOYLUUN
    XGXIAWAWBWNYRYMUUOXGXIBWAWBWCXPABUCVSWDWEUUEUUGWFWDWGWHWIWJABEWKURUFWL $.
    $( [19-Sep-2007] $) $( [17-Sep-2007] $)
  $}

  ${
    $d A u x y w $.  $d B a b u v x y z $.  $d X a b x y z $.
    fgsb.1 $e |- B = { x | E. y
                  ( y (_ A /\ y e. Fin /\ x = |^| y ) } $.
    $( Filter generated by a subbasis ` A ` .  Bourbaki TG I.37 paragraph
       above prop. 1. The theorem has been slightly modified because
       the definitions of the empty set are different in Bourbaki and
       Metamath. $)
    fgsb   $p |- ( ( A (_ P~ X /\ X e. V /\ A =/= (/) ) -> ( -. (/) e. B ->
      { x e. P~ X | E. y e. B y (_ x } e. Fil ) ) $=
      ( va vb vz vu vw vv cpw wss cvv wcel c0 wne w3a wn cv wrex crab cfil wa
      cuni wi wal cin wral wceq sseq2 rexbidv elrab ss0 eleq1d biimpcd
      r19.23aiv adantl sylbi con3i ump 3ad2ant2 r19.2z cfn cint wex cab fine
      neeq1i sylibr 3ad2ant3 ax-17 eleq1 sseq1 imbi12d eleq2i fiiu chvar uniss
      3ad2ant1 sylan9ssr unipw syl6ss visset elpw pm3.27 ssid jctir rcla4ev
      syl jca cbvrexv syl5bb jctil ssuni r19.21aiva sylanc adantr hbrab1 hbuni
      hbpw hbss hbrex hbre1 hbrab hbeq rexbid elrabf unissb elpwi mprgbir
      sspwb mpbi sseli sylbir r19.41v sstr r19.22si adantll 3adant2 syl3an1b
      gen2 a1i inpws1 rcla4e biimpi anim12i infi1 eqcomi 3syl ss2in syl2an
      an4s expcom r19.23aiva com12 imp syl2anb pm3.26 sseq2d rexbidva rgen2
      3jca wb pwexg rabexg eqid isfil mpbird ex ) CEMZNZEOPZCQRZSZQDPZTZBUAZAUA
      ZNZBDUBZAUULUCZUDPZUUPUURUEZUVDQUVCPZTZUVCUFZUVCPZUEZGUAZUVCPZHUAZUVHNZUV
      KUVMNZSZUVMUVCPZUGZHUHGUHZUVKUVMUIZUVCPZHUVCUJGUVCUJZSZUVEUVJUVSUWBUVEUVG
      UVIUURUVGUUPUVFUUQUVFQUULPZUUSQNZBDUBZUEUUQUVBUWFAQUULUUTQUKUVAUWEBDUUTQU
      USULUMUNUWFUUQUWDUWEUUQBDUWEUUSDPZUUQUWEUUSQDUUSUOUPUQURUSUTVAUSUVEUVHUUL
      PZUUSUVHNZBDUBZUEZUVIUUPUWKUURUUPUWHUWJUUNUUMUWHUUOUVBAEOVBVCDQRZUWIBDUJU
      WJUUPUWIBDVDUUOUUMUWLUUNUUOUUSCNUUSVEPUUTUUSVFUKSBVGAVHZQRUWLABCVIDUWMQFV
      JVKVLUUPUWIBDUUPUWGUEZUUSUUSNZUUSUVCPZUEUWIUWNUWPUWOUWNUUSUULPZIUAZUUSNZI
      DUBZUEUWPUWNUWQUWTUWNUUSENUWQUWNUUSUULUFZEUWGUUPUUSCUFZUXAJUAZDPZUXCUXBNZ
      UGUWGUUSUXBNZUGZJBUXGJVMUXCUUSUKUXDUWGUXEUXFUXCUUSDVNUXCUUSUXBVOVPUXDUXCU
      WMPZUXEDUWMUXCFVQZABUXCCVRUTVSUUMUUNUXBUXANUUOCUULVTWAWBEWCWDUUSEBWEWFVKU
      WNUWGUWOUEUWTUWNUWGUWOUUPUWGWGUUSWHZWIUWSUWOIUUSDUWRUUSUUSVOWJWKWLUVBUWTA
      UUSUULUUTUUSUKZUWRUUTNZIDUBUWTUVBUXKUXLUWSIDUUTUUSUWRULUMUVAUXLBIDUUSUWRU
      UTVOWMWNUNVKUXJWOUUSUUSUVCWPWKWQWRWLWSUVBUWJAIUVHUULAIUVCUVBAIUULWTXAZAIE
      UWREPAVMXBUWIABDUWGAVMAIUUSUVHUWRUUSPAVMUXMXCXDUUTUVHUKUVAUWIBDBKIUUTUVHK
      UAUUTPBVMBIUVCUVBBAIUULUVABDXEUWRUULPZBVMXFXAXGUUTUVHUUSULXHXIVKWLUVSUVEU
      VRGHUVPUVMUULPZUUSUVMNZBDUBZUEZUVQUVKUULPZUUSUVKNZBDUBZUEZUVNUVOUXRUVLUYB
      UVNUVOSUXOUXQUVNUYBUXOUVOUVNUVMUVHMZPUXOUVMUVHHWEWFUYCUULUVMUVHENZUYCUULN
      UYDUWRENZIUVCIUVCEXJUWRUVCPUXNUUSUWRNZBDUBZUEUYEUVBUYGAUWRUULUUTUWRUKUVAU
      YFBDUUTUWRUUSULUMUNUXNUYEUYGUWREXKWSUTXLUVHEXMXNXOXPVCUYBUVOUXQUVNUYAUVOU
      XQUXSUYAUVOUEUXTUVOUEZBDUBUXQUXTUVOBDXQUYHUXPBDUUSUVKUVMXRXSXPXTYAWLUVBUY
      AAUVKUULUUTUVKUKZUVAUXTBDUUTUVKUUSULUMUNYBUVBUXQAUVMUULUUTUVMUKZUVAUXPBDU
      UTUVMUUSULUMUNVKYCYDUWBUVEUWAGHUVCUVCUYBUXRUWAUVLUVQUYBUXRUEUVTUULPZUUSUV
      TNZBDUBZUEZUWAUXSUXOUYAUXQUYNUXSUXOUEUYKUYAUXQUEUYMUXSUYKUXOUVKUVMEYEWSUX
      CUVKNZJDUBZLUAZUVMNZLDUBZUYMUYAUXQUYPUYSUYMUYOUYSUYMUGJDUYSUXDUYOUEZUYMUY
      RUYTUYMUGLDUYTUYQDPZUYRUEUYMUXDVUAUYOUYRUYMUXCUYQUIZDPZVUBUVTNZUYMUXDVUAU
      EZUYOUYRUEUYLVUDBVUBDVUDBVMUUSVUBUVTVOYFVUEUXHUYQUWMPZUEVUBUWMPZVUCUXDUXH
      VUAVUFUXDUXHUXIYGVUAVUFDUWMUYQFVQYGYHABUXCUYQCYIVUGVUCUWMDVUBDUWMFYJVQYGY
      KUXCUVKUYQUVMYLYMYNYOYPYQYPYRUXTUYOBJDUUSUXCUVKVOWMUXPUYRBLDUUSUYQUVMVOWM
      YSYHYNUVBUYMAUVTUULUUTUVTUKZUVAUYLBDVUHUWGUEUUTUVTUUSVUHUWGYTUUAUUBUNVKUV
      BUYAAUVKUULUYIUVAUXTBDUYIUWGUEUUTUVKUUSUYIUWGYTUUAUUBUNUVBUXQAUVMUULUYJUV
      AUXPBDUYJUWGUEUUTUVMUUSUYJUWGYTUUAUUBUNYSUUCYDUUDUVEUVCOPZUVDUWCUUEUUPVUI
      UURUUNUUMVUIUUOUUNUULOPVUIEOUUFUVBAUULOUUGWKVCWSGHOUVCUVHUVHUUHUUIWKUUJUU
      K $.
      $( [2-Sep-2008] $) $( [16-Nov-2007] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d X x y z $.
    efilcp.1 $e |- B = { z | E. y
                  ( y (_ A /\ y e. Fin /\ z = |^| y ) } $.
    $( A filter containing a set ` A ` exists iff ` A ` has the finite
      intersection property (i.e. no finite intersection of elements of ` A `
      is empty).  Bourbaki TG I.37 prop. 1. $)
    efilcp  $p |- ( ( A (_ P~ X /\ X e. V /\ A =/= (/) ) ->
      ( -. (/) e. B <-> E. x e. Fil A (_ x ) ) $=
      ( cpw wss cvv wcel c0 wne w3a wn cv cfil wrex crab wa sseq2 rcla4ev fgsb
      imp wral 3simp1 adantr cfn cint wceq wex cab abfi sseqtr4i ssel2 ssid
      sseq1 mpan2 syl mpan rgen jctir ssrab sylibr sylanc ex wi sstr df-ne
      fipfil2 pm2.27 necom biimpi sylbir syl6 3exp com34 com4t com14 com13
      com23 imp31 inteq int0 eqeq1i vn0 neeq1 mpbii sylbi eqcoms necomd sylib
      a1d pm2.61d2 nan mpbir df-3an notbii nexdv 0ex eqeq1 3anbi3d exbidv
      elab2 exp31 r19.23aiv com12 impbid ) DFHZIZFJKZDLMZNZLEKZOZDAPZIZAQRZXMXO
      XRBPZCPZIZBERZCXISZQKZDYCIZXRXMXOTZXQYEAYCQXPYCDUAUBXMXOYDCBDEFGUCUDYFXJY
      BCDUEZTYEYFXJYGXMXJXOXJXKXLUFUGYBCDDEIZXTDKZYBDXSDIZXSUHKZXTXSUIZUJZNZBUK
      ZCULECBDUMGUNYHYITXTEKZYBDEXTUOYPXTXTIZYBXTUPYAYQBXTEXSXTXTUQUBURUSUTVAVB
      YBCXIDVCVDVEVFXRXMXOXQXMXOVGAQXPQKZXQXMXOYRXQTXMTZYJYKLYLUJZNZBUKZOXOYSUU
      ABYSYJYKTZYTTZOZUUAOYSUUEVGYSUUCTYTOZVGYSUUCUUFYSXSLUJZUUCUUFVGZYRXQXMUUG
      OZUUHVGZYRXMXQUUJYRXMXQUUJVGUUCXQUUIYRXMTZUUFYJYKXQUUIUUKUUFVGZVGZVGYJXQY
      KUUMYJXQYKUUMVGZYJXQTXSXPIZUUNXSDXPVHUUIYKUUOUULUUIXSLMZYKUUOUULVGVGXSLVI
      UUKYKUUOUUPUUFYRYKUUOUUPUUFVGVGVGZXMYRUUOUUPYKNZYLLMZVGZUUQXSXPVJUUOUUPUU
      TYKUUFUUOUUPYKUUTUUFUUOUUPYKUUTUUFVGUURUUTUUSUUFUURUUSVKUUSLYLMZUUFLYLVLU
      VAUUFLYLVIZVMVNVOVPVQVRUSUGVSVNVTUSVFWAUDVSVFWAWBUUGUUFUUCUUGUVAUUFUUGYLL
      UUSLXSLXSUJLUIZYLUJZUUSLXSWCUVDJYLUJZUUSUVCJYLWDWEUVEJLMUUSWFJYLLWGWHWIUS
      WJWKUVBWLWMWNUDYSUUCYTWOWPUUAUUDYJYKYTWQWRVDWSXNUUBYOUUBCLEWTXTLUJZYNUUAB
      UVFYMYTYJYKXTLYLXAXBXCGXDWRVDXEXFXGXH $.
      $( [2-Sep-2008] $) $( [20-Nov-2007] $)
  $}

  ${ $d A y $.  $d F x y $.
    $( A filter is closed under taking finite intersections. $)
    filint2 $p |- ( F e. Fil -> ( ( A (_ F /\ A =/= (/) /\ A e. Fin )
           -> |^| A e. F ) ) $=
      ( vy vx cfil wcel wss c0 wne cfn cint wi cpw elpw2g cv wceq sseq1 neeq1
      eleq1 inteq eleq1d imbi12d imbi2d w3a wn cuni wa wal cin wral eqid isfil
      3simp3 fiint sylib 19.21bi syl6bi pm2.43i 3expd com12 vtoclg syl6bir
      com4l com13 3impd ) BEFZABGZAHIZAJFZAKZBFZVFVGVHVIVKLZLZLVGVFVFVMVGVFVFVM
      LZLVFVGVGVFVMVFVGABMZFVGVNLZABENCOZBGZVFVQHIZVQJFZVQKZBFZLZLZLZLVPCAVOVQA
      PZVRVGWEVNVQABQWFWDVMVFWFVSVHWCVLVQAHRWFVTVIWBVKVQAJSWFWAVJBVQATUAUBUBUCU
      BVFVRWDVFVRVSVTWBVFVRVSVTUDWBLZVFVFHBFUEBUFZBFUGZVQBFDOZWHGVQWJGUDWJBFLDU
      HCUHZVQWJUIBFDBUJCBUJZUDZWGCDEBWHWHUKULWMWGCWMWLWGCUHWIWKWLUMCDBUNUOUPUQU
      RUSUTVAVBVCURVDURVE $.
      $( [2-Sep-2008] $) $( [27-Apr-2008] $)
  $}


  ${ $d A y z $.  $d B y $.  $d C y z $.
    fisub.1 $e |- B = { z | E. y ( y (_ A /\ y e. Fin /\ z = |^| y ) } $.
    fisub.2 $e |- D = { z | E. y ( y (_ C /\ y e. Fin /\ z = |^| y ) } $.
    $( If a set has the finite intersection property, its subsets have also
       this property. $)
    fisub $p |- ( C (_ A -> ( -. (/) e. B -> -. (/) e. D ) ) $=
      ( wss c0 wcel cv cfn cint wceq w3a wex wi wa sstr 0ex eqeq1 3anbi3d
      exbidv elab2 biimpri 19.23bi 3exp syl expcom com4l 3imp 19.23aiv com12
      syl5ib con3d ) ECIZJFKZJDKZUQALZEIZUTMKZJUTNZOZPZAQZUSURVFUQUSVEUQUSRZAVA
      VBVDVGUQVAVBVDUSVAUQVBVDUSRRZVAUQSUTCIZVHUTECTVIVBVDUSVIVBVDPZUSAUSVJAQZV
      IVBBLZVCOZPZAQVKBJDUAVLJOZVNVJAVOVMVDVIVBVLJVCUBZUCUDGUEUFUGUHUIUJUKULUMU
      NVAVBVMPZAQVFBJFUAVOVQVEAVOVMVDVAVBVPUCUDHUEUOUP $.
      $( [2-Sep-2008] $) $( [27-Apr-2008] $)
  $}

  ${ $d A a b u v y $.  $d A t x y $.  $d A x y z $.  $d X a b x y $.
     $d X t x y $.  $d X x y z $.  $d w x y $.
    $( Filter generated by a subbasis ` A ` .  Bourbaki TG I.37 paragraph
       above prop. 1.  (The theorem has been slightly modified because
       the definitions of the empty set are different in Bourbaki and
       Metamath.) $)
    fgsb2 $p |- ( ( A (_ P~ X /\ X e. V /\ A =/= (/) ) ->
      ( -. (/) e. ( fi ` A ) ->
         { x e. P~ X | E. y e. ( fi ` A ) y (_ x } e. Fil ) ) $=
      ( va vb vz vw vt vu vv cpw wss cvv wcel c0 wne w3a cfi cfv wn cv wrex
      crab cfil wa cuni wi wal cin wral wceq sseq2 rexbidv elrab ss0 eleq1d
      biimpcd r19.23aiv adantl sylbi con3i ump 3ad2ant2 r19.2z fine2 ssexg
      3simp1 pwexg sylanc 3simp3 sylc fiiu2 syl imp sspwuni sylib adantr sstrd
      visset elpw sylibr pm3.27 ssid jctir sseq1 rcla4ev jca cbvrexv syl5bb
      jctil ssuni r19.21aiva hbrab1 hbuni ax-17 hbpw hbss hbrex hbre1 hbrab
      hbeq rexbid elrabf unissb elpwi mprgbir sspwb mpbi sseli sylbir r19.41v
      sstr r19.22si adantll 3adant2 syl3an1b gen2 a1i inpws1 rcla4e cdm elfvdm
      infi exp3a mpcom ss2in syl2an an4s expcom r19.23aiva com12 syl2anb
      anim12i pm3.26 sseq2d rexbidva rgen2 3jca wb rabexg eqid isfil mpbird ex
      ) CDLZMZDNOZCPQZRZPCSTZOZUAZBUBZAUBZMZBUUKUCZAUUFUDZUEOZUUJUUMUFZUUSPUURO
      ZUAZUURUGZUUROZUFZEUBZUUROZFUBZUVCMZUVFUVHMZRZUVHUUROZUHZFUIEUIZUVFUVHUJZ
      UUROZFUURUKEUURUKZRZUUTUVEUVNUVQUUTUVBUVDUUMUVBUUJUVAUULUVAPUUFOZUUNPMZBU
      UKUCZUFUULUUQUWAAPUUFUUOPULUUPUVTBUUKUUOPUUNUMUNUOUWAUULUVSUVTUULBUUKUVTU
      UNUUKOZUULUVTUUNPUUKUUNUPUQURUSUTVAVBUTUUTUVCUUFOZUUNUVCMZBUUKUCZUFZUVDUU
      JUWFUUMUUJUWCUWEUUHUUGUWCUUIUUQADNVCVDUUKPQZUWDBUUKUKUWEUUJUWDBUUKVECNOZU
      UIUWGUUJCNVFUUGUUFNOZUWHUUJCUUFNVGZUUGUUHUUIVHZUUHUUGUWIUUIDNVIZVDZVJUUGU
      UHUUIVKVLUUJUWDBUUKUUJUWBUFZUUNUUNMZUUNUUROZUFUWDUWNUWPUWOUWNUUNUUFOZGUBZ
      UUNMZGUUKUCZUFUWPUWNUWQUWTUWNUUNDMUWQUWNUUNCUGZDUUJUWBUUNUXAMZUUGUWIUWBUX
      BUHZUUJUUGUWIUFUWHUXCUWJUUNCNVMVNUWKUWMVJVOUUJUXADMZUWBUUJUUGUXDUWKCDVPVQ
      VRVSUUNDBVTWAWBUWNUWBUWOUFUWTUWNUWBUWOUUJUWBWCUUNWDZWEUWSUWOGUUNUUKUWRUUN
      UUNWFWGVNWHUUQUWTAUUNUUFUUOUUNULZUWRUUOMZGUUKUCUWTUUQUXFUXGUWSGUUKUUOUUNU
      WRUMUNUUPUXGBGUUKUUNUWRUUOWFWIWJUOWBUXEWKUUNUUNUURWLVNWMVJWHVRUUQUWEAGUVC
      UUFAGUURUUQAGUUFWNWOZAGDUWRDOAWPWQUWDABUUKUWBAWPAGUUNUVCUWRUUNOAWPUXHWRWS
      UUOUVCULUUPUWDBUUKBHGUUOUVCHUBUUOOBWPBGUURUUQBAGUUFUUPBUUKWTUWRUUFOBWPXAW
      OXBUUOUVCUUNUMXCXDWBWHUVNUUTUVMEFUVKUVHUUFOZUUNUVHMZBUUKUCZUFZUVLUVFUUFOZ
      UUNUVFMZBUUKUCZUFZUVIUVJUXLUVGUXPUVIUVJRUXIUXKUVIUXPUXIUVJUVIUVHUVCLZOUXI
      UVHUVCFVTWAUXQUUFUVHUVCDMZUXQUUFMUXRIUBZDMZIUURIUURDXEUXSUUROUXSUUFOZUUNU
      XSMZBUUKUCZUFUXTUUQUYCAUXSUUFUUOUXSULUUPUYBBUUKUUOUXSUUNUMUNUOUYAUXTUYCUX
      SDXFVRVAXGUVCDXHXIXJXKVDUXPUVJUXKUVIUXOUVJUXKUXMUXOUVJUFUXNUVJUFZBUUKUCUX
      KUXNUVJBUUKXLUYDUXJBUUKUUNUVFUVHXMXNXKXOXPWHUUQUXOAUVFUUFUUOUVFULZUUPUXNB
      UUKUUOUVFUUNUMUNUOXQUUQUXKAUVHUUFUUOUVHULZUUPUXJBUUKUUOUVHUUNUMUNUOWBXRXS
      UVQUUTUVPEFUURUURUXPUXLUVPUVGUVLUXPUXLUFUVOUUFOZUUNUVOMZBUUKUCZUFZUVPUXMU
      XIUXOUXKUYJUXMUXIUFUYGUXOUXKUFUYIUXMUYGUXIUVFUVHDXTVRJUBZUVFMZJUUKUCZKUBZ
      UVHMZKUUKUCZUYIUXOUXKUYMUYPUYIUYLUYPUYIUHJUUKUYPUYKUUKOZUYLUFZUYIUYOUYRUY
      IUHKUUKUYRUYNUUKOZUYOUFUYIUYQUYSUYLUYOUYIUYKUYNUJZUUKOZUYTUVOMZUYIUYQUYSU
      FUYLUYOUFUYHVUBBUYTUUKVUBBWPUUNUYTUVOWFYAUYQUYSVUACSYBZOZUYQUYSVUAUHUYKCS
      YCVUDUYQUYSVUAUYKUYNCVUCYDYEYFVOUYKUVFUYNUVHYGYHYIYJYKYLYKVOUXNUYLBJUUKUU
      NUYKUVFWFWIUXJUYOBKUUKUUNUYNUVHWFWIYMYNYIUUQUYIAUVOUUFUUOUVOULZUUPUYHBUUK
      VUEUWBUFUUOUVOUUNVUEUWBYOYPYQUOWBUUQUXOAUVFUUFUYEUUPUXNBUUKUYEUWBUFUUOUVF
      UUNUYEUWBYOYPYQUOUUQUXKAUVHUUFUYFUUPUXJBUUKUYFUWBUFUUOUVHUUNUYFUWBYOYPYQU
      OYMYRXSYSUUTUURNOZUUSUVRYTUUJVUFUUMUUHUUGVUFUUIUUHUWIVUFUWLUUQAUUFNUUAVNV
      DVREFNUURUVCUVCUUBUUCVNUUDUUE $.
      $( [27-Apr-2008] $) $( [27-Apr-2008] $)
  $}

  ${
    $d A x y z $.  $d X x y z $.
    $( A filter containing a set ` A ` exists iff ` A ` has the finite
       intersection property (i.e. no finite intersection of elements of ` A `
       is empty).  Bourbaki TG I.37 prop. 1. $)
    efilcp2  $p |- ( ( A (_ P~ X /\ X e. V /\ A =/= (/) ) ->
        ( -. (/) e. ( fi ` A )  <-> E. x e. Fil A (_ x ) ) $=
      ( vy vz cpw wss cvv wcel c0 wne w3a cfi cfv wn cv cfil wrex crab wa
      sseq2 rcla4ev fgsb2 imp wral 3simp1 adantr wi wb pwexg elpw2g syl
      biimprd elisset a1d syl6com 3imp abfi2 ssel2 ssid sseq1 mpan2 r19.21aiva
      jca ssrab sylibr sylanc ex cfn cint wceq wex 0ex sppfi mpan notbid sstr
      df-ne fipfil2 pm2.27 necom biimpi sylbir syl6 3exp com34 com4t com14
      com13 com23 inteq int0 eqeq1i vn0 neeq1 mpbii sylbi eqcoms necomd sylib
      pm2.61d2 nan mpbir df-3an notbii nexdv syl5bir 3expd com24 mpcom
      r19.23aiv com12 impbid ) BCFZGZCHIZBJKZLZJBMNZIZOZBAPZGZAQRZXRYAYDDPZEPZG
      ZDXSRZEXNSZQIZBYIGZYDXRYATZYCYKAYIQYBYIBUAUBXRYAYJEDBCUCUDYLXOYHEBUEZTYKY
      LXOYMXRXOYAXOXPXQUFUGYLBHIZYMXRYNYAXOXPXQYNXPXOBXNFZIZXQYNUHXPYPXOXPXNHIY
      PXOUICHUJBXNHUKULUMYPYNXQBYOUNUOUPUQZUGYNBXSGZYMBHURYRYHEBYRYFBITYFXSIZYH
      BXSYFUSYSYFYFGZYHYFUTYGYTDYFXSYEYFYFVAUBVBULVCULULVDYHEXNBVEVFVGVHYDXRYAY
      CXRYAUHAQXRYCYBQIZYAYNXRYCUUAYAUHUHYQYNUUAYCXRYAYNUUAYCXRYAYNYAYEBGZYEVII
      ZJYEVJZVKZLZDVLZOUUAYCXRLZYNXTUUGJHIYNXTUUGUIVMDJHBHVNVOVPUUHUUFDUUHUUBUU
      CTZUUETZOZUUFOUUHUUKUHUUHUUITUUEOZUHUUHUUIUULUUHYEJVKZUUIUULUHZUUAYCXRUUM
      OZUUNUHZUUAXRYCUUPUUAXRYCUUPUHUUIYCUUOUUAXRTZUULUUBUUCYCUUOUUQUULUHZUHZUH
      UUBYCUUCUUSUUBYCUUCUUSUHZUUBYCTYEYBGZUUTYEBYBVQUUOUUCUVAUURUUOYEJKZUUCUVA
      UURUHUHYEJVRUUQUUCUVAUVBUULUUAUUCUVAUVBUULUHUHUHZXRUUAUVAUVBUUCLZUUDJKZUH
      ZUVCYEYBVSUVAUVBUVFUUCUULUVAUVBUUCUVFUULUVAUVBUUCUVFUULUHUVDUVFUVEUULUVDU
      VEVTUVEJUUDKZUULJUUDWAUVGUULJUUDVRZWBWCWDWEWFWGULUGWHWCWIULVHWJUDWHVHWJUQ
      UUMUULUUIUUMUVGUULUUMUUDJUVEJYEJYEVKJVJZUUDVKZUVEJYEWKUVJHUUDVKZUVEUVIHUU
      DWLWMUVKHJKUVEWNHUUDJWOWPWQULWRWSUVHWTUOXAUDUUHUUIUUEXBXCUUFUUJUUBUUCUUEX
      DXEVFXFXGXHXIXJWIXKXLXM $.
      $( [2-Sep-2008] $) $( [27-Apr-2008] $)
  $}

  ${
    $d A g $.  $d A x y $.  $d F g $.  $d F x y $.
    $( Condition to have a filter finer than a given filter and containing a
       set ` A ` .  Bourbaki T.G. I.37 cor. 1 $)
    cnfilca  $p |- ( ( F e. Fil /\ A (_ U. F /\ A =/= (/) )
      -> ( E. g e. Fil ( A e. g /\  F (_ g )
         <-> A. x e. F ( x i^i A ) =/= (/) ) ) $=
      ( vy cfil wcel cuni wss c0 wne w3a cv wa wrex csn cun cfi cfv wn cin
      wral cvv wb ssexg uniexg sylan2 ancoms 3adant3 snssg anbi1d unss syl6bb
      syl rexbidv cpw efilcp2 wi ex elpwg bitr3d pwuni biimpi mpan2 syl6bi
      syl6 pm2.43a mpan9 3ad2ant1 emnfil wceq nelneq pm3.27 con3i un00
      necon3bbii sylib syl3anc cfn cint wex elisset snex jctil unexb 0ex sppfi
      notbid wal ax-17 cpr elun2 adantl snidg elun1 adantr jca prssg bicomd
      3simp2 mpbird sylanc prfi jctir prex a1i sseq1 eleq1 anbi12d inteq
      eqeq2d imbi12d cla4gv mpid com23 imp31 df-ne sylibr necomd ad2antrr
      visset intprd eqcomd neeq1d r19.21ai cdif moec ssundif int0 eqtr ineq2
      inv1 neeq1 necom sylbi eqcoms com3l mpi syl6com ax-mp com12 com14
      3ad2ant3 imp difss ssfi 3simp1 filint2 ineq1 rcla4v incom biimpd 3expd
      com13 sylbir 3imp com3r pm2.61i elsni con3d 19.21aiv disj1 disjssun vn0
      mpbi neeq2 mpbii fipfil2 3exp2 pm2.61ine com24 impbida alnex df-3an
      notbii imnan bitr4i albii bitr3i syl5bb bitrd 3bitr2d ) DFGZBDHZIZBJKZLZB
      CMZGZDUWHIZNZCFOBPZDQZUWHIZCFOZJUWMRSGZTZAMZBUAZJKZADUBZUWGUWKUWNCFUWGBUC
      GZUWKUWNUDUWCUWEUXBUWFUWEUWCUXBUWEUWDUCGZUXBUWCBUWDUCUEZDFUFZUGUHUIZUXBUW
      KUWLUWHIZUWJNUWNUXBUWIUXGUWJBUWHUCUJUKUWLDUWHULUMUNUOUWMUWDUPZIZUXCUWMJKZ
      UWQUWOUDUWGCUWMUWDUQUWCUWEUXIUWFUWCUXCUWEUXIUXEUXCUWEUXIUWEUXCUXBUWEUXIUR
      UWEUXCUXBUXDUSUXBUWEUWLUXHIZUXIUXBBUXHGZUWEUXKBUWDUCUTZBUXHUCUJVAUXKDUXHI
      ZUXIDVBUXKUXNNUXIUWLDUXHULVCVDVEVFVGVHUIUWCUWEUXCUWFUXEVIUWCUWEUXJUWFUWCJ
      FGTZUXJVJUWCUXONDJVKZTZUXJDJFVLUXQUWLJVKZUXPNZTUXJUXSUXPUXRUXPVMVNUXSUWMJ
      UWLDVOVPVQUNVDVIVRUWGUWQEMZUWMIZUXTVSGZJUXTVTZVKZLZEWAZTZUXAUWGJUCGZUWMUC
      GZNZUWQUYGUDUWGUYIUYHUWGUWLUCGZDUCGZNUYIUWGUYLUYKUWCUWEUYLUWFDFWBVIBWCWDU
      WLDWEVQWFWDUYJUWPUYFEJUCUWMUCWGWHUNUWGUYAUYBNZUYDTZURZEWIZUXAUYGUWGUYPUXA
      UWGUYPNZUWTADUYQAWJUYQUWRDGZUWTUYQUYRNZUWTUWRBWKZVTZJKUYSJVUAUYSJVUAVKZTZ
      JVUAKUWGUYPUYRVUCUWGUYRUYPVUCUWGUYRUYPVUCURUWGUYRNZUYPUYTUWMIZUYTVSGZNZVU
      CVUDVUEVUFVUDVUEUWRUWMGZBUWMGZNZVUDVUHVUIUYRVUHUWGUWRDUWLWLWMUWGVUIUYRUWG
      UXBVUIUXFUXBBUWLGVUIBUCWNBUWLDWOUNUNWPWQUYRUXLVUEVUJUDVUDUYRUXLNVUJVUEUWR
      BUWMDUXHWRWSUWGUYRVMUWGUXLUYRUWGUXLUWEUWCUWEUWFWTUWGUXBUXLUWEUDUXFUXMUNXA
      WPXBXAUWRBXCXDVUDUYTUCGZUYPVUGVUCURZURVUKVUDUWRBXEXFUYOVULEUYTUCUXTUYTVKZ
      UYMVUGUYNVUCVUMUYAVUEUYBVUFUXTUYTUWMXGUXTUYTVSXHXIVUMUYDVUBVUMUYCVUAJUXTU
      YTXJXKWHXLXMUNXNUSXOXPJVUAXQXRXSUYSUWSVUAJUYSVUAUWSUYSUWRUCGZUXBNVUAUWSVK
      UYSUXBVUNUWGUXBUYPUYRUXFXTAYAWDUWRBYBUNYCYDXAUSYEUWGUXANZUYOEBUXTGZVUOUYO
      URZVUPUYCBUXTUWLYFZVTZUAZVKZVUQUXTBYGUYMVUOVVAUYNUYAUYBVUOVVAUYNURZURZUYA
      VURDIZUYBVVCURZUXTUWLDYHVURJVKZVVDVVEURZVUOVVDUYBVVFVVBUWGUXAVVDUYBVVFVVB
      URZURURZUWFUWCUXAVVIURUWEUYBUXAVVDUWFVVHUXAVVDUWFVVHURZURZURUYBVVKUXAVVJV
      VDVVFUWFVVBVVFVUSJVTZVKZUWFVVBURZVURJXJVVLUCVKZVVMVVNURYIVVMVVOVUSUCVKZVV
      NVVMVVOVVPVUSVVLUCYJUSVVPVUTBUCUAZVKZVVNVUSUCBYKVVRVVQBVKZVVNBYLVVRVVSVUT
      BVKZVVNVVRVVSVVTVUTVVQBYJUSVVAVVTUWFUYNVVAVVTUYCBVKZUWFUYNURZVVAVVTVWAUYC
      VUTBYJUSVWBBUYCBUYCVKUWFUYCJKZUYNBUYCJYMVWCJUYCKZUYNUYCJYNVWDUYNJUYCXQZVC
      YOZVEYPVFYQVFYRUNYSYTUNUUAXFXFXFUUBUUCUUDUUBVVFTZVURUXTIZVVGUXTUWLUUEUYBV
      WHVVDVWGVVCUYBVWHVURVSGZVVDVWGVVCURURUYBVWHVWIUXTVURUUFUSVUOVVDVWGVWIVVBU
      WGUWCUXAVVDVWGVWIVVBURZURURUWCUWEUWFUUGUXAUWCVVDVWGVWJUWCVVDVWGLVWIUXAVVB
      UWCVVDVWGVWIUXAVVBURZURZVWGVVDUWCVWLVWGVURJKZVVDUWCVWLURURVURJXQUWCVVDVWM
      VWLUWCVVDVWMVWIVWKUWCVVDVWMVWILVUSDGZVWKVURDUUHVWNUXAVUSBUAZJKZVVBUWTVWPA
      VUSDUWRVUSVKUWSVWOJUWRVUSBUUIYDUUJVWOVUTVKZVWPVVBURVUSBUUKVWQVWPVUTJKZVVB
      VWOVUTJYMVVAVWRVWCUYNVWRVWCURVUTUYCVUTUYCVKVWRVWCVUTUYCJYMUULYPVWFYSVEYTV
      FVFUUMUUNUUOUUNUUPUUQUUMVHUUBVFUUBYRUURYOUUDUUNUNUYMVUOVUPTZUYNUYAUYBVUOV
      WSUYNURURUYAVWSVUOUYBUYNVWSUYAUXTDIZVUOUYBUYNURZURVWSUXTUWLUAJVKZUYAVWTUR
      VWSUWRUXTGZUWRUWLGZTZURZAWIVXBVWSVXFAVXCVWSVXEVXCVXDVUPVXDVXCVUPVXDUWRBVK
      ZVXCVUPURUWRBUUSVXGVXCVUPUWRBUXTXHUULUNUUAUUTUUAUVAAUXTUWLUVBXRVXBUYAVWTU
      XTUWLDUVCUULUNVUOVWTVXAUWGVWTVXAURZUXAUWCUWEVXHUWFUWCVXHURUXTJUYBUWCVWTUX
      TJVKZUYNUWCVWTVXIUYNURZURZURUYBVXKUWCVXJVWTVXIUYCVVLVKZUYNUXTJXJVXLVVOUYN
      YIVXLVVOUYCUCVKZUYNVXLVVOVXMUYCVVLUCYJUSUYNUCUYCUCUYCVKZVWDUYNVXNJUCKZVWD
      UCJKVXOUVDUCJYNUVEUCUYCJUVFUVGVWEVQYPVFYRUNXFXFXFUUBUWCVWTUXTJKZVXAUWCVWT
      VXPUYBUYNUWCVWTVXPUYBLZNZVWDUYNVXRUYCJUWCVXQVWCUXTDUVHUUDXSVWEVQUVIUUQUVJ
      VIWPUUAYSUVKUUDUUNUURUVAUVLUYGUYETZEWIUYPUYEEUVMVXSUYOEVXSUYMUYDNZTUYOUYE
      VXTUYAUYBUYDUVNUVOUYMUYDUVPUVQUVRUVSUVTUWAUWB $.
      $( [2-Sep-2008] $) $( [27-Apr-2008] $)
  $}

  ${
    $d A x $.  $d B b x $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem1 $p |- ( B e. C
        -> ( B e. { x | E. b (  b (_ A /\ b e. Fin /\ x = ( A \ b ) ) }
                 <-> E. b ( b (_ A /\ b e. Fin /\ B = ( A \ b ) ) ) ) $=
      ( cv wss cfn wcel cdif wceq w3a wex eqeq1 3anbi3d exbidv elabg ) EFZBGZRH
      IZAFZBRJZKZLZEMSTCUBKZLZEMACDUACKZUDUFEUGUCUESTUACUBNOPQ $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b x $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem2 $p |- ( -. A e. Fin -> -. (/) e. { x | E. b
        (  b (_ A /\ b e. Fin /\ x = ( A \ b ) ) }  ) $=
      ( cfn wcel wn cv wss c0 cdif wceq w3a wex cab wi ssdif0 eqcom bitri wa
      eqss eleq1 biimpd sylbir ex com23 com13 3imp 19.23aiv con3i cvv wb 0ex
      rcfpfillem1 notbid ax-mp sylibr ) BDEZFCGZBHZURDEZIBURJZKZLZCMZFZIUSUTAGV
      AKLCMANEZFZVDUQVCUQCUSUTVBUQVBUTUSUQVBBURHZUTUSUQOOVHVAIKVBBURPVAIQRVHUSU
      TUQVHUSUTUQOZVHUSSZURBKZVIVKBURKVJURBQBURTRVKUTUQURBDUAUBUCUDUEUCUFUGUHUI
      IUJEZVGVEUKULVLVFVDABIUJCUMUNUOUP $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b x y $.  $d B b y $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem3 $p |- ( A e. B -> U. { x | E. b
         (  b (_ A /\ b e. Fin /\ x = ( A \ b ) ) } = A ) $=
      ( vy wcel cv wss cfn cdif wceq w3a wex cab cuni wa wi 19.42v bicomi
      exbii excom 3simp3 adantl pm3.26 jca 3simpa 19.22i 19.40 cvv difexg
      eleq2 ceqsexgv eldifi syl6bi syl com12 adantr 3syl 19.23aiv sylbi eqeq1
      3anbi3d exbidv anbi12d ancom c0 0ex 0ss emfin dif0 eqcomi 3pm3.2i sseq1
      eleq1 difeq2 eqeq2d 3anbi123d cla4egv mp2 mpbiran syl6bb impbid eluniab
      syl5bb eqrdv ) BCFZEDGZBHZWGIFZAGZBWGJZKZLZDMZANOZBWFEGZWJFZWNPZAMZWPBFZW
      PWOFWFWSWTWSWFWTWSWQWMPZDMZAMZWFWTQZWRXBAXBWRWQWMDRSTXCXAAMZDMXDXAADUAXEX
      DDXEWLWQPZWHWIPZPZAMXFAMZXGAMZPXDXAXHAXAXFXGXAWLWQWMWLWQWHWIWLUBUCWQWMUDU
      EWMXGWQWHWIWLUFUCUEUGXFXGAUHXIXDXJWFXIWTWFWKUIFZXIWTQBWGCUJXKXIWPWKFZWTWQ
      XLAWKUIWJWKWPUKULWPBWGUMUNUOUPUQURUSUTUTUPWRWTABCWJBKZWRWTWHWIBWKKZLZDMZP
      ZWTXMWQWTWNXPWJBWPUKXMWMXODXMWLXNWHWIWJBWKVAVBVCVDXQXPWTWTXPVEVFUIFVFBHZV
      FIFZBBVFJZKZLZXPVGXRXSYABVHVIXTBBVJVKVLXOYBDVFUIWGVFKZWHXRWIXSXNYAWGVFBVM
      WGVFIVNYCWKXTBWGVFBVOVPVQVRVSVTWAVRWBWNAWPWCWDWE $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A a b c u v x $.  $d B u v $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem4 $p |- ( A e. B
        -> A. u e. { x | E. b ( b (_ A /\ b e. Fin /\ x = ( A \ b ) ) }
           A. v e. { x | E. b ( b (_ A /\ b e. Fin /\ x = ( A \ b ) ) }
       ( u i^i v ) e. { x | E. b ( b (_ A /\ b e. Fin /\ x = ( A \ b ) ) } ) $=
      ( va vc wcel cv cin wss cfn cdif wceq w3a wex cab wa wi cun an6 unss
      biimpi unfi ineq12 difundi syl6eqr 3anim123i sylbi expcom visset unex
      sseq1 eleq1 difeq2 eqeq2d 3anbi123d cla4ev syl6 19.23aiv com12 imp
      cbvexv syl2anb adantl cvv wb inex1 rcfpfillem1 ax-mp sylibr ex bi2anan9
      mp2an syl5ib r19.21aivv ) DEIZCJZBJZKZFJZDLZWBMIZAJDWBNZOPFQARZIZCBWFWFVR
      WCWDVSWEOZPZFQZWCWDVTWEOZPZFQZSZWGVSWFIZVTWFIZSZVRWNWGVRWNSWCWDWAWEOZPZFQ
      ZWGWNWTVRGJZDLZXAMIZVSDXANZOZPZGQZHJZDLZXHMIZVTDXHNZOZPZHQZWTWJWMXGXNWTXF
      XNWTTGXNXFWTXMXFWTTHXMXFXAXHUAZDLZXOMIZWADXONZOZPZWTXFXMXTXFXMSXBXISZXCXJ
      SZXEXLSZPXTXBXCXEXIXJXLUBYAXPYBXQYCXSYAXPXAXHDUCUDXAXHUEYCWAXDXKKXRVSXDVT
      XKUFDXAXHUGUHUIUJUKWSXTFXOXAXHGULHULUMWBXOOZWCXPWDXQWRXSWBXODUNWBXOMUOYDW
      EXRWAWBXODUPUQURUSUTVAVBVAVCWIXFFGWBXAOZWCXBWDXCWHXEWBXADUNWBXAMUOYEWEXDV
      SWBXADUPUQURVDWLXMFHWBXHOZWCXIWDXJWKXLWBXHDUNWBXHMUOYFWEXKVTWBXHDUPUQURVD
      VEVFWAVGIWGWTVHVSVTCULZVIADWAVGFVJVKVLVMVSVGIZVTVGIZWQWNVHYGBULYHWOWJYIWP
      WMADVSVGFVJADVTVGFVJVNVOVPVQ $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b x $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem5 $p |- ( A e. B
         -> A e. { x | E. b ( b (_ A /\ b e. Fin /\ x = ( A \ b ) ) } ) $=
      ( wcel cv wss cfn cdif wceq w3a wex cab c0 cvv 0ex 0ss emfin cin in0
      disj3 mpbi 3pm3.2i sseq1 eleq1 difeq2 eqeq2d 3anbi123d cla4egv mp2
      rcfpfillem1 mpbiri ) BCEBDFZBGZUMHEZAFBUMIZJKDLAMEUNUOBUPJZKZDLZNOENBGZNH
      EZBBNIZJZKZUSPUTVAVCBQRBNSNJVCBTBNUAUBUCURVDDNOUMNJZUNUTUOVAUQVCUMNBUDUMN
      HUEVEUPVBBUMNBUFUGUHUIUJABBCDUKUL $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b c u v x $.
    $( Lemma for ~ rcfpfil . $)
    rcfpfillem6 $p |- ( ( u e. { x | E. b ( b (_ A /\ b e. Fin
                 /\ x = ( A \ b ) ) } /\ v (_ A /\ u (_ v )
           -> v e. { x | E. b ( b (_ A /\ b e. Fin /\ x = ( A \ b ) ) } ) $=
      ( vc cv wss cfn wcel cdif wceq w3a wex cab wi cvv wb visset rcfpfillem1
      sseq1 eleq1 difeq2 eqeq2d 3anbi123d cbvexv syl6bb ax-mp difexg cla4ev
      ssdifss 3ad2ant1 difss ssfi mpan2 3ad2ant2 wa dfss4 eqcomd eqeq1d biimpd
      cun undif sylbi difun1 syl5eqr difeq1 syl5cbi syl6com a1d 3imp impcom
      difeq2d ex com13 eqcoms com12 syl5ib syl3anc 3exp 19.23aiv eqeq1 3anbi3d
      exbidv elab sylibr ) CGZEGZDHZWHIJZAGZDWHKZLZMZENZAOZJZBGZDHZWGWRHZMWIWJW
      RWLLZMZENZWRWPJWQWSWTXCWQFGZDHZXDIJZWGDXDKZLZMZFNZWSWTXCPPZWGQJZWQXJRCSXL
      WQWIWJWGWLLZMZENXJADWGQETXNXIEFWHXDLZWIXEWJXFXMXHWHXDDUAWHXDIUBXOWLXGWGWH
      XDDUCUDUEUFUGUHXIXKFXIWSWTXCXDWRWGKZKZDHZXQIJZWRDXQKZLZXCXIWSWTMXBXRXSYAM
      EXQXDQJXQQJFSXDXPQUIUHWHXQLZWIXRWJXSXAYAWHXQDUAWHXQIUBYBWLXTWRWHXQDUCUDUE
      UJXIWSXRWTXEXFXRXHXDDXPUKULULXIWSXSWTXFXEXSXHXFXQXDHXSXDXPUMXDXQUNUOUPULX
      IWSWTYAXIDDWRKZKZWRLZWTYAPZWSYEXIYFXIYFPWRYDWTXIWRYDLZYAWTXIYGYAPWTXIUQZY
      GYAYHYDXTWRYHYCXQDXIWTYCXQLZXEXFXHWTYIPZXEDXGKZXDLZXFXHYJPZPXDDURYLYMXFXH
      YLDWGKZXDLZYJXHYLYOXHYKYNXDXHYNYKWGXGDUCUSUTVAWTYNXPKZXQLYIYOWTYPYCXQWTDW
      GXPVBZKZYCYPWTYQWRLYRYCLWGWRVCYQWRDUCVDDWGXPVEVFUTYNXDXPVGVHVIVJVDVKVLVMU
      DVAVNVOVPVQWRDURVRVKVSVTWAVDVKWOXCAWRBSWKWRLZWNXBEYSWMXAWIWJWKWRWLWBWCWDW
      EWF $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

  ${
    $d A b u v x y $.  $d B b u v x $.
    $( Relative complements of the finite parts of an infinite set is a
       filter. When ` A = NN ` the set of the relative complements is called
       Frechet's filter and is used to define the concept of limit of
       a sequence. $)
    rcfpfil $p |- ( ( A e. B /\ -. A e. Fin )
          -> { x | E. b ( b (_ A /\ b e. Fin /\ x = ( A \ b ) ) } e. Fil ) $=
      ( vu vv vy wcel cfn wn wa cv wss cdif wceq w3a wex cab cfil c0 cuni wi
      wal cin wral rcfpfillem2 adantl rcfpfillem3 rcfpfillem5 eqeltrd adantr
      jca rcfpfillem6 unissb visset eqeq1 3anbi3d exbidv elab difss sseq1
      mpbiri 3ad2ant3 19.23aiv sylbi mprgbir sstr2 mpi syl3an2 a1i 19.21aivv
      rcfpfillem4 3jca wb cvv copab crn cpw elpw2g bicomd 3anbi1d opabbidv
      pwexg opabex2g syl 3simpb ssopab2i jctil ssexg rnexg rnopab syl5eqelr
      eqid isfil mpbird ) BCHZBIHJZKZDLZBMZWSIHZALZBWSNZOZPZDQZARZSHZTXGHJZXGUA
      ZXGHZKZELZXGHZFLZXJMZXMXOMZPXOXGHZUBZFUCEUCZXMXOUDXGHFXGUEEXGUEZPZWRXLXTY
      AWRXIXKWQXIWPABDUFUGWPXKWQWPXJBXGABCDUHABCDUIUJUKULWRXSEFXSWRXNXOBMZXQXRX
      PAFEBDUMXPXJBMZYCYDGLZBMZGXGGXGBUNYEXGHWTXAYEXCOZPZDQZYFXFYIAYEGUOXBYEOZX
      EYHDYJXDYGWTXAXBYEXCUPUQURUSYHYFDYGWTYFXAYGYFXCBMBWSUTYEXCBVAVBVCVDVEVFXO
      XJBVGVHVIVJVKWPYAWQAFEBCDVLUKVMWPXHYBVNZWQWPXGVOHYKWPXEDAVPZVQZVOXGWPYLVO
      HYMVOHWPYLWSBVRZHZXAXDPZDAVPZVOWPXEYPDAWPWTYOXAXDWPYOWTWSBCVSVTWAWBWPYQYO
      XDKZDAVPZMZYSVOHZKYQVOHWPUUAYTWPYNVOHUUABCWCDAYNXCVOWDWEYPYRDAYOXAXDWFWGW
      HYQYSVOWIWEUJYLVOWJWEXEDAWKWLEFVOXGXJXJWMWNWEUKWO $.
      $( [2-Sep-2008] $) $( [14-Jul-2008] $)
  $}

