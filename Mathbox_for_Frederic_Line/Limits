$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d F a b l $.  $d F a b x $.  $d J a b l $.  $d X a b l $.  $d X a b x $.
    limfillem1.1 $e |- J e. Top $.
    limfillem1.2 $e |- X = U. J $.
    limfillem1.3 $e |- X = U. F $.
    $( Lemma for ~ plimfil . $)
    limfillem1 $p |- ( F e. Fil ->
      ( ( fLim1 ` J ) ` F ) =
        { l e. X | ( ( nei ` J ) ` { l } ) C_ F } ) $=
      ( vx va vb cfil wcel cuni wceq cflim1 cfv cv csn cnei wss crab wa unieq
      eqeq2d elrab sseq2 rabbisdv w3a copab ctop sfvlim ax-mp df-3an eqeq1d
      eqcom syl5bb bicomi anbi1i bitri opabbii eqtri cvv uniexg eqeltri rabex
      fvopab4 sylbir mpan2 ) AKLZCAMZNZABOPZPDQRBSPPZATZDCUAZNZGVIVKUBACHQZMZNZ
      HKUAZLVPVSVKHAKVQANVRVJCVQAUCUDUEIJAVMIQZTZDCUAZVOVTVLWAANWBVNDCWAAVMUFUG
      VLWAKLZWAMZCNZJQWCNZUHZIJUIZWAVTLZWGUBZIJUIBUJLZVLWINEBCIJDFUKULWHWKIJWHW
      DWFUBZWGUBWKWDWFWGUMWMWJWGWJWMVSWFHWAKVQWANZVRCNWFVSWNVRWECVQWAUCUNCVRUOU
      PUEUQURUSUTVAVNDCCBMZVBFWLWOVBLEBUJVCULVDVEVFVGVH $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${
    $d F l $.  $d J l $.  $d X l $.
    limfillem2.1 $e |- X = U. J $.
    limfillem2.2 $e |- X = U. F $.
    $( Lemma for ~ plimfil . $)
    limfillem2 $p |- ( ( J e. Top /\ F e. Fil ) ->
        ( ( fLim1 ` J ) ` F ) =
          { l e. X | ( ( nei ` J ) ` { l } ) C_ F } ) $=
      ( ctop wcel cfil cflim1 cfv cv csn cnei wss crab wceq wi c0 cpr cif cuni
      fveq2 fveq1d rabeq ax-mp a1i unieq syl sseq1d rabbisdv 3eqtrd eqeq12d
      imbi2d eleq1 sseq2 imbi12d indistop elimel eqid eqtr3i wa eqtr ex eqcoms
      mpan imp syl2an iftrue eqcomd sylanc wn cun 0ex p0ex unipr un0 uncom
      unisn 3eqtr4i eqtri expcom imp3a iffalse pm2.61i limfillem1 dedth2v ) BGH
      ZAIHZABJKZKZDLMZBNKZKZAOZDCPZQZWHWIWQRWIAWHBSSMZTZUAZJKZKZWLWTNKZKZAOZDWT
      UBZPZQZRWHAWRMZUAZIHZXJXAKZXDXJOZDXFPZQZRBAWSXIBWTQZWQXHWIXPWKXBWPXGXPAWJ
      XABWTJUCUDXPWPWODBUBZPZWODXFPZXGWPXRQZXPCXQQXTEWODCXQUEUFUGXPXQXFQZXRXSQB
      WTUHZWODXQXFUEUIXPWOXEDXFXPWNXDAXPWLWMXCBWTNUCUDUJUKULUMUNAXJQZWIXKXHXOAX
      JIUOYCXBXLXGXNAXJXAUCYCXEXMDXFAXJXDUPUKUMUQXJWTXFDBWSGWRURUSXFUTWHXFXJUBZ
      QZXPYCYEWHYAAUBZYDQZYEXPYCYAYGYEYFXQQZYAYGYERZCYFXQFEVAYHYAVBYFXFQYIYFXQX
      FVCYIXFYFXFYFQYGYEXFYFYDVCVDVEUIVFVGYBAXJUHVHWHWTBWHBWSVIVJWHXJAWHAXIVIVJ
      VKWTWSQZXJXIQZYEWHVLXFWSUBZQZYDXIUBZQZYEYJYKYLYNQZYMYOVBYERYLSWRVMZYNSWRV
      NVOVPWRSVMWRYQYNWRVQSWRVRWRVOVSVTWAYPYMYOYEYMYPYOYERZYMYPVBXFYNQYRXFYLYNV
      CYRYNXFYOYNXFQZYEYOYSVBYDXFYDYNXFVCVJWBVEUIWBWCUFWTWSUHXJXIUHVHWHBWSWDWHA
      XIWDVKWEWFWGVG $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${
    $d F l $.  $d J l $.  $d L l $.  $d X l $.
    plimfil.1 $e |- X = U. J $.
    plimfil.2 $e |- X = U. F $.
    $( The predicate "is a limit of a filter". $)
    plimfil $p |- ( ( J e. Top /\ F e. Fil /\ L e. X ) ->
    ( L e. ( ( fLim1 ` J ) ` F ) <-> ( ( nei ` J ) ` { L } ) C_ F ) ) $=
      ( vl ctop wcel cfil cflim1 cfv csn cnei wss wa wb cv crab limfillem2
      eleq2d wceq sneq fveq2d sseq1d elrab syl6bb 3adant3 ompfl3 ) BHIZAJIZCDIZ
      CABKLLZIZCMZBNLZLZAOZUJUKUNULURPZQULUJUKPZUNCGRZMZUPLZAOZGDSZIUSUTUMVECAB
      DGEFTUAVDURGCDVACUBZVCUQAVFVBUOUPVACUCUDUEUFUGUHUI $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${
    $d F a $.  $d J a $.  $d L a $.  $d X a $.  $d Y a $.
    limvinlv.1 $e |- X = U. J $.
    limvinlv.2 $e |- Y = U. L $.
    limvinlv.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    $( The limit value of a convergent function whose values are in a Hausdorff
       space belongs to the set of the limit values. $)
    limvinlv $p |- ( ( ( J e. Haus /\ L e. Fil /\ F : Y --> X ) /\
         ( ( J fLimf L ) ` F ) =/= (/) )
         -> A e. ( ( J fLimf L ) ` F ) ) $=
      ( va cv cflimf co cfv wcel weu cha cfil wf w3a c0 wne wa 3simpl2 3simpl1
      3simpl3 3jca holimf2 sylancom wreu wi pm4.24 eubii df-reu bitr4i cin
      crab wceq cuni csn eqtr inidm dfin5 unieq sneq fvex uniex snid eleq2
      eleq1 biimpd eqcoms ax-mp syl6bi mpi a1d syl expcom ex mp2 reuunisn
      sylancr sylbi mpcom ) JKBCDLMZNZOZJPZCQOZDROZFEBSZTZWFUAUBZUCZAWFOZWLWMWJ
      WIWKTWHWNWJWIWKWIWJWKWMUDWIWJWKWMUEWIWJWKWMUFUGJBCDEFHGUHUIWHWGJWFUJZWNWO
      UKZWHWGWGUCZJPWPWGWRJWGULUMWGJWFUNUOWFWFUPZWGJWFUQZURZWTWTUSZUTZURZWQWPXA
      XDUCWSXCURZWQWSWTXCVAWSWFURZXEWQUKZWFVBZXGWFWSWFWSURZXEWQXIXEUCWFXCURZWQW
      FWSXCVAXFXAXJWQUKZXHJWFWFVCZXAXKUKWFWSXIXAXKXIXAUCWFWTURZXKWFWSWTVAXMWFUS
      ZXBURZXKWFWTVDXOXNUTZXCURXKXNXBVEXKXCXPXJXCXPURZWQXJXQUCWFXPURZWQWFXCXPVA
      XRWOWNXRXNXPOZWOXNWFBWEVFVGVHXSWOUKXPWFXPWFURXSXNWFOZWOXPWFXNVIAXNURXTWOU
      KZIYAXNAXNAURXTWOXNAWFVJVKVLVMVNVLVOVPVQVRVLVQVQVQVSVLVTVQVSVLVMVQXLWGJWF
      WAWBWCWD $.
      $( [6-Dec-2010] $) $( [14-Nov-2010] $)
  $}

  ${
    $d F s $.  $d L s $.  $d N s $.  $d X s $.  $d Y s $.
    flimfneih.1 $e |- X = U. J $.
    flimfneih.2 $e |- Y = U. L $.
    flimfneih.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    $( A neighborhood of the limit value of a convergent function whose values
       are in a Hausdorff space contains the image of a filter element. $)
    flimfneih $p |- ( ( ( J e. Haus /\ L e. Fil /\ F : Y --> X )
       /\ ( ( J fLimf L ) ` F ) =/= (/)
       /\ N e. ( ( nei ` J ) ` { A } ) ) -> E. s e. L ( F " s ) C_ N ) $=
      ( cha wcel cfil wf w3a cflimf co cfv csn cnei cv cima wss wrex c0 wne
      ctop flimfneii haustop 3anim1i syl3an1 limvinlv 3adant3 syld3an2 ) CLMZDN
      MZGFBOZPZABCDQRSZMZEATCUASSMZBHUBUCEUDHDUEZUTUFUGZCUHMZUQURPVAVBVCUSABCDE
      FGHIJUIUPVEUQURCUJUKULUSVDVAVBABCDFGIJKUMUNUO $.
      $( [6-Dec-2010] $) $( [18-Nov-2010] $)
  $}

