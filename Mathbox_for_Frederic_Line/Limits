$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c fLim1 $.

  $( Extend class notation with a function returning the limit of a filter. $)
  cflim1 $a class fLim1 $.

  $c fLim2 $.

  $( Extend class notation with a function returning the limit of a function. $)
  cflim2 $a class fLim2 $.

  ${ $d x y a b l $.
    $( Define a function (indexed by a topology ` x `) whose value is the
       limits of a filter ` a ` . $)
    df-flim1 $a |- fLim1 = { <. x , y >. | ( x e. Top /\ y = { <. a , b >. |
      ( a e. Fil /\ U. a = U. x /\
        b = { l e. U. x | ( ( nei ` x ) ` { l } ) (_ a } ) } ) } $.
  $}

  ${ $d x y z a b l $.
    $( Define a function (indexed by a topology ` x ` and a filter ` y `)
       whose values are the limits of a function ` a ` . $)
    df-flim2 $a |- fLim2 = { <. <. x , y >. , z >. | ( x e. Top /\ y e. Fil /\
      z = { <. a , b >. | ( a : U. y --> U. x /\
        b = { l | ( ( nei ` x ) ` l ) (_ ( a " y ) } ) } ) } $.
  $}

  ${ $d J a b l x y $.  $d X a b l x y $.
    sfvlim.1 $e |- X = U. J $.
    $( Functions whose values are the limits of the filters. $)
    sfvlim $p |- ( J e. Top -> ( fLim1 ` J ) = { <. a , b >. |
      ( a e. Fil /\ U. a = X /\
        b = { l e. X | ( ( nei ` J ) ` { l } ) (_ a } ) } ) $=
      ( vx vy ctop wcel cv cfil cuni wceq csn cnei cfv wss crab w3a copab cvv
      cflim1 cpw cin wa ssexg wi wal simpll eqimss sspwuni visset elpw biimpri
      sylbir syl ad2antlr jca elin sylibr ex 3adant3 impcom 3simp3 adantl
      19.21aivv ssopab2 uniexg syl5eqel pwexg 3syl inex1g incom eleq1i biimpi
      opabex2g sylanc unieq syl6eqr eqeq2d rabeq fveq2 fveq1d sseq1d rabbisdv
      eqtrd 3anbi23d opabbidv df-flim1 fvopab4g mpdan ) AIJZCKZLJZWNMZBNZDKZEKO
      ZAPQZQZWNRZEBSZNZTZCDUAZUBJZAUCQXFNXFWNLBUDZUDZUEZJZXDUFZCDUAZRZXMUBJZXGW
      MXFXMUBUGWMXEXLUHZDUICUIXNWMXPCDWMXEXLWMXEUFXKXDXEWMXKWOWQWMXKUHXDWOWQUFZ
      WMXKXQWMUFZWOWNXIJZUFXKXRWOXSWOWQWMUJWQXSWOWMWQWPBRZXSWPBUKXTWNXHRZXSWNBU
      LXSYAWNXHCUMUNUOUPUQURUSWNLXIUTVAVBVCVDXEXDWMWOWQXDVEVFUSVBVGXEXLCDVHVAWM
      XJUBJZXOWMXIUBJZXILUEZUBJZYBWMBUBJXHUBJYCWMAMZUBBAIVIFVJBUBVKXHUBVKVLXILU
      BVMYEYBYDXJUBXILVNVOVPVLCDXJXCUBVQUQVRGHAWOWPGKZMZNZWRWSYGPQZQZWNRZEYHSZN
      ZTZCDUAXFIUBUCYGANZYOXECDYPYIWQYNXDWOYPYHBWPYPYHYFBYGAVSFVTZWAYPYMXCWRYPY
      MYLEBSZXCYPYHBNYMYRNYQYLEYHBWBUQYPYLXBEBYPYKXAWNYPWSYJWTYGAPWCWDWEWFWGWAW
      HWIGHCDEWJWKWL $.
      $( [1-Sep-2008] $) $( [17-Aug-2008] $)
  $}

  ${ $d F a b l $. $d F a b x $. $d J a b l $. $d X a b l $.
     $d X a b x $.
    limfillem1.1 $e |- J e. Top $.
    limfillem1.2 $e |- X = U. J $.
    limfillem1.3 $e |- X = U. F $.
    $( Lemma for ~ plimfil . $)
    limfillem1 $p |- ( F e. Fil ->
      ( ( fLim1 ` J ) ` F ) =
        { l e. X | ( ( nei ` J ) ` { l } ) (_ F } ) $=
      ( vx va vb cfil wcel cuni wceq cflim1 cfv cv csn cnei wss crab wa unieq
      eqeq2d elrab sseq2 rabbisdv w3a copab ctop sfvlim ax-mp df-3an eqeq1d
      eqcom syl5bb bicomi anbi1i bitri opabbii eqtri cvv uniexg eqeltri rabex
      fvopab4 sylbir mpan2 ) AKLZCAMZNZABOPZPDQRBSPPZATZDCUAZNZGVIVKUBACHQZMZNZ
      HKUAZLVPVSVKHAKVQANVRVJCVQAUCUDUEIJAVMIQZTZDCUAZVOVTVLWAANWBVNDCWAAVMUFUG
      VLWAKLZWAMZCNZJQWCNZUHZIJUIZWAVTLZWGUBZIJUIBUJLZVLWINEBCIJDFUKULWHWKIJWHW
      DWFUBZWGUBWKWDWFWGUMWMWJWGWJWMVSWFHWAKVQWANZVRCNWFVSWNVRWECVQWAUCUNCVRUOU
      PUEUQURUSUTVAVNDCCBMZVBFWLWOVBLEBUJVCULVDVEVFVGVH $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${ $d F l $. $d J l $. $d X l $.
    limfillem2.1 $e |- X = U. J $.
    limfillem2.2 $e |- X = U. F $.
    $( Lemma for ~ plimfil . $)
    limfillem2 $p |- ( ( J e. Top /\ F e. Fil ) ->
        ( ( fLim1 ` J ) ` F ) =
          { l e. X | ( ( nei ` J ) ` { l } ) (_ F } ) $=
      ( ctop wcel cfil cflim1 cfv cv csn cnei wss crab wceq wi c0 cpr cif cuni
      fveq2 fveq1d rabeq ax-mp a1i unieq syl sseq1d rabbisdv 3eqtrd eqeq12d
      imbi2d eleq1 sseq2 imbi12d indistop elimel eqid eqtr3i wa eqtr ex eqcoms
      mpan imp syl2an iftrue eqcomd sylanc wn cun 0ex p0ex unipr un0 uncom
      unisn 3eqtr4i eqtri expcom imp3a iffalse pm2.61i limfillem1 dedth2v ) BGH
      ZAIHZABJKZKZDLMZBNKZKZAOZDCPZQZWHWIWQRWIAWHBSSMZTZUAZJKZKZWLWTNKZKZAOZDWT
      UBZPZQZRWHAWRMZUAZIHZXJXAKZXDXJOZDXFPZQZRBAWSXIBWTQZWQXHWIXPWKXBWPXGXPAWJ
      XABWTJUCUDXPWPWODBUBZPZWODXFPZXGWPXRQZXPCXQQXTEWODCXQUEUFUGXPXQXFQZXRXSQB
      WTUHZWODXQXFUEUIXPWOXEDXFXPWNXDAXPWLWMXCBWTNUCUDUJUKULUMUNAXJQZWIXKXHXOAX
      JIUOYCXBXLXGXNAXJXAUCYCXEXMDXFAXJXDUPUKUMUQXJWTXFDBWSGWRURUSXFUTWHXFXJUBZ
      QZXPYCYEWHYAAUBZYDQZYEXPYCYAYGYEYFXQQZYAYGYERZCYFXQFEVAYHYAVBYFXFQYIYFXQX
      FVCYIXFYFXFYFQYGYEXFYFYDVCVDVEUIVFVGYBAXJUHVHWHWTBWHBWSVIVJWHXJAWHAXIVIVJ
      VKWTWSQZXJXIQZYEWHVLXFWSUBZQZYDXIUBZQZYEYJYKYLYNQZYMYOVBYERYLSWRVMZYNSWRV
      NVOVPWRSVMWRYQYNWRVQSWRVRWRVOVSVTWAYPYMYOYEYMYPYOYERZYMYPVBXFYNQYRXFYLYNV
      CYRYNXFYOYNXFQZYEYOYSVBYDXFYDYNXFVCVJWBVEUIWBWCUFWTWSUHXJXIUHVHWHBWSWDWHA
      XIWDVKWEWFWGVG $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

  ${ $d F l $. $d J l $. $d L l $. $d X l $.
    plimfil.1 $e |- X = U. J $.
    plimfil.2 $e |- X = U. F $.
    $( The predicate "is a limit of a filter". $)
    plimfil $p |-  ( ( J e. Top /\ F e. Fil /\ L e. X ) ->
    ( L e. ( ( fLim1 ` J ) ` F ) <-> ( ( nei ` J ) ` { L } ) (_ F ) ) $=
      ( vl ctop wcel cfil cflim1 cfv csn cnei wss wa wb cv crab limfillem2
      eleq2d wceq sneq fveq2d sseq1d elrab syl6bb 3adant3 ompfl3 ) BHIZAJIZCDIZ
      CABKLLZIZCMZBNLZLZAOZUJUKUNULURPZQULUJUKPZUNCGRZMZUPLZAOZGDSZIUSUTUMVECAB
      DGEFTUAVDURGCDVACUBZVCUQAVFVBUOUPVACUCUDUEUFUGUHUI $.
      $( [28-Jan-2009] $) $( [5-Jan-2009] $)
  $}

