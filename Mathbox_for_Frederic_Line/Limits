$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d F a b l $.  $d F a b x $.  $d J a b l $.  $d X a b l $.  $d X a b x $.
    limfillem1.1 $e |- J e. Top $.
    limfillem1.2 $e |- X = U. J $.
    limfillem1.3 $e |- X = U. F $.
    $( Lemma for ~ plimfil . $)
    limfillem1 $p |- ( F e. Fil ->
      ( ( fLim1 ` J ) ` F ) =
        { l e. X | ( ( nei ` J ) ` { l } ) C_ F } ) $=
      ( vx va vb cfil wcel cuni wceq cflim1 cfv cv csn cnei wss crab wa unieq
      eqeq2d elrab sseq2 rabbidv w3a copab ctop sfvlim ax-mp df-3an eqcom
      eqeq1d syl5bb bicomi anbi1i bitri opabbii eqtri cvv uniexg eqeltri rabex
      fvopab4 sylbir mpan2 ) AKLZCAMZNZABOPZPDQRBSPPZATZDCUAZNZGVIVKUBACHQZMZNZ
      HKUAZLVPVSVKHAKVQANVRVJCVQAUCUDUEIJAVMIQZTZDCUAZVOVTVLWAANWBVNDCWAAVMUFUG
      VLWAKLZWAMZCNZJQWCNZUHZIJUIZWAVTLZWGUBZIJUIBUJLZVLWINEBCIJDFUKULWHWKIJWHW
      DWFUBZWGUBWKWDWFWGUMWMWJWGWJWMVSWFHWAKVSVRCNVQWANZWFCVRUNWNVRWECVQWAUCUOU
      PUEUQURUSUTVAVNDCCBMZVBFWLWOVBLEBUJVCULVDVEVFVGVH $.
      $( [5-Jan-2009] $)
  $}

  ${
    $d F l $.  $d J l $.  $d X l $.
    limfillem2.1 $e |- X = U. J $.
    limfillem2.2 $e |- X = U. F $.
    $( Lemma for ~ plimfil . $)
    limfillem2 $p |- ( ( J e. Top /\ F e. Fil ) ->
        ( ( fLim1 ` J ) ` F ) =
          { l e. X | ( ( nei ` J ) ` { l } ) C_ F } ) $=
      ( ctop wcel cfil cflim1 cfv cv csn cnei wss crab wceq wi c0 cpr cif cuni
      fveq2 fveq1d rabeq ax-mp a1i unieq syl sseq1d rabbidv 3eqtrd eqeq12d
      imbi2d eleq1 sseq2 imbi12d indistop elimel eqid iftrue eqcomd eqtr3i wa
      eqtr ex eqcoms mpan imp syl2an syl11anc wn iffalse cun 0ex p0ex unipr
      un0 uncom unisn 3eqtr4i eqtri expcom imp3a pm2.61i limfillem1 dedth2v )
      BGHZAIHZABJKZKZDLMZBNKZKZAOZDCPZQZWHWIWQRWIAWHBSSMZTZUAZJKZKZWLWTNKZKZAOZ
      DWTUBZPZQZRWHAWRMZUAZIHZXJXAKZXDXJOZDXFPZQZRBAWSXIBWTQZWQXHWIXPWKXBWPXGXP
      AWJXABWTJUCUDXPWPWODBUBZPZWODXFPZXGWPXRQZXPCXQQXTEWODCXQUEUFUGXPXQXFQZXRX
      SQBWTUHZWODXQXFUEUIXPWOXEDXFXPWNXDAXPWLWMXCBWTNUCUDUJUKULUMUNAXJQZWIXKXHX
      OAXJIUOYCXBXLXGXNAXJXAUCYCXEXMDXFAXJXDUPUKUMUQXJWTXFDBWSGWRURUSXFUTWHXFXJ
      UBZQZWHXPYCYEWHWTBWHBWSVAVBWHXJAWHAXIVAVBXPYAAUBZYDQZYEYCYBAXJUHYAYGYEYFX
      QQZYAYGYERZCYFXQFEVCYHYAVDYFXFQYIYFXQXFVEYIXFYFXFYFQYGYEXFYFYDVEVFVGUIVHV
      IVJVKWHVLWTWSQZXJXIQZYEWHBWSVMWHAXIVMYJXFWSUBZQZYDXIUBZQZYEYKWTWSUHXJXIUH
      YLYNQZYMYOVDYERYLSWRVNZYNSWRVOVPVQWRSVNWRYQYNWRVRSWRVSWRVPVTWAWBYPYMYOYEY
      MYPYOYERZYMYPVDXFYNQYRXFYLYNVEYRYNXFYOYNXFQZYEYOYSVDYDXFYDYNXFVEVBWCVGUIW
      CWDUFVJVKWEWFWGVI $.
      $( [5-Jan-2009] $)
  $}

  ${
    $d F l $.  $d J l $.  $d L l $.  $d X l $.
    plimfil.1 $e |- X = U. J $.
    plimfil.2 $e |- X = U. F $.
    $( The predicate "is a limit of a filter".  (Contributed by FL,
       5-Jan-2009.) $)
    plimfil $p |- ( ( J e. Top /\ F e. Fil /\ L e. X ) ->
    ( L e. ( ( fLim1 ` J ) ` F ) <-> ( ( nei ` J ) ` { L } ) C_ F ) ) $=
      ( vl ctop wcel cfil cflim1 cfv csn cnei wss wa wb cv crab limfillem2
      eleq2d wceq sneq fveq2d sseq1d elrab syl6bb 3adant3 3anibar ) BHIZAJIZCDI
      ZCABKLLZIZCMZBNLZLZAOZUJUKUNULURPZQULUJUKPZUNCGRZMZUPLZAOZGDSZIUSUTUMVECA
      BDGEFTUAVDURGCDVACUBZVCUQAVFVBUOUPVACUCUDUEUFUGUHUI $.
      $( [5-Jan-2009] $)
  $}

  ${
    $d F a $.  $d J a $.  $d L a $.  $d X a $.  $d Y a $.
    limvinlv.1 $e |- X = U. J $.
    limvinlv.2 $e |- Y = U. L $.
    limvinlv.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    $( The limit value of a convergent function whose values are in a Hausdorff
       space belongs to the set of the limit values.  (Contributed by FL,
       14-Nov-2010.) $)
    limvinlv $p |- ( ( ( J e. Haus /\ L e. Fil /\ F : Y --> X ) /\
         ( ( J fLimf L ) ` F ) =/= (/) )
         -> A e. ( ( J fLimf L ) ` F ) ) $=
      ( va cv cflimf co cfv wcel weu cha cfil wf w3a c0 wne wa simpl2 simpl1
      simpl3 3jca holimf2 sylancom wreu wi pm4.24 eubii df-reu bitr4i cin crab
      wceq cuni csn dfin5 reuunisn inidm eqtr unieq sneq fvex uniex snid eleq2
      eleq1 biimpd eqcoms ax-mp syl6bi mpi a1d syl expcom ex mp2 mpsyl sylancr
      sylbi mpcom ) JKBCDLMZNZOZJPZCQOZDROZFEBSZTZWGUAUBZUCZAWGOZWMWNWKWJWLTWIW
      OWKWJWLWJWKWLWNUDWJWKWLWNUEWJWKWLWNUFUGJBCDEFHGUHUIWIWHJWGUJZWOWPUKZWIWHW
      HUCZJPWQWHWSJWHULUMWHJWGUNUOWQWGWGUPZWHJWGUQZURZXAXAUSZUTZURZWRJWGWGVAZWH
      JWGVBWTWGURZXBXEUCWTXDURZWRWGVCZWTXAXDVDXHWRUKWGWTWGWTURZXHWRXJXHUCWGXDUR
      ZWRWGWTXDVDXGXBXKWRUKZXIXFXBXLUKWGWTXJXBXLXJXBUCWGXAURZXLWGWTXAVDXMWGUSZX
      CURZXLWGXAVEXOXNUTZXDURXLXNXCVFXLXDXPXKXDXPURZWRXKXQUCWGXPURZWRWGXDXPVDXR
      WPWOXRXNXPOZWPXNWGBWFVGVHVIXSWPUKXPWGXPWGURXSXNWGOZWPXPWGXNVJAXNURXTWPUKZ
      IYAXNAXNAURXTWPXNAWGVKVLVMVNVOVMVPVQVRVSVMVRVRVRVTVMWAVRVTVMWBWCWDWE $.
      $( [14-Nov-2010] $)
  $}

  ${
    $d F s $.  $d L s $.  $d N s $.  $d X s $.  $d Y s $.
    flimfneih.1 $e |- X = U. J $.
    flimfneih.2 $e |- Y = U. L $.
    flimfneih.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    $( A neighborhood of the limit value of a convergent function whose values
       are in a Hausdorff space contains the image of a filter element.
       (Contributed by FL, 18-Nov-2010.) $)
    flimfneih $p |- ( ( ( J e. Haus /\ L e. Fil /\ F : Y --> X )
       /\ ( ( J fLimf L ) ` F ) =/= (/)
       /\ N e. ( ( nei ` J ) ` { A } ) ) -> E. s e. L ( F " s ) C_ N ) $=
      ( cha wcel cfil wf w3a cflimf co cfv c0 wne csn cnei cv cima wss wrex
      limvinlv 3adant3 ctop haustop 3anim1i flimfneii syl3an1 syld3an2 ) CLMZDN
      MZGFBOZPZABCDQRSZMZUTTUAZEAUBCUCSSMZBHUDUEEUFHDUGZUSVBVAVCABCDFGIJKUHUIUS
      CUJMZUQURPVAVCVDUPVEUQURCUKULABCDEFGHIJUMUNUO $.
      $( [18-Nov-2010] $)
  $}

  ${
    limfilnei.1 $e |- X = U. J $.
    limfilnei.2 $e |- F = ( ( nei ` J ) ` { A } ) $.
    $( ` A ` is a limit of the filter of the neighborhoods of ` A ` .
       (Contributed by FL, 27-May-2011.) $)
    limfilnei $p |- ( ( J e. Top /\ A e. X )
      -> A e. ( ( fLim1 ` J ) ` F ) ) $=
      ( ctop wcel wa cflim1 cfv csn wss c0 wne w3a cnei cfil wi simpl snssi
      adantl snnzg 3jca neifil cuni wceq simp1 3ad2ant3 jca unnei ssid wb eqid
      isfillim ibar bicomd sylan9bb 3exp1 com34 3imp com12 eqcoms imp mpbiri
      ex 3syl pm2.43i fveq2i syl6eleqr 3exp com23 ) CGHZADHZIZABCJKZKZHZVOVMALZ
      DMZVSNOZPVSCQKKZRHZVOVRSVOVMVTWAVMVNTVNVTVMADUAZUBVNWAVMADUCUBUDVSCDEUEVO
      WCVRVMVNWCVRSVMWCVNVRVMWCVNVRVMWCVNPZAWBVPKZVQWEAWFHZWEVMVTIWBUFZDUGZWEWG
      SWEVMVTVMWCVNUHVNVMVTWCWDUIUJVSCDEUKWIWEWGWIWEIWGWBWBMZWBULWIWEWGWJUMZWEW
      KSDWHWEDWHUGZWKVMWCVNWLWKSVMWCWLVNWKVMWCWLVNWKVMWCWLPWGVNWJIZVNWJAWBCDWHE
      WHUNUOVNWJWMVNWJUPUQURUSUTVAVBVCVDVEVFVGVHBWBVPFVIVJVKVLVDVBVGVH $.
      $( [27-May-2011] $)
  $}

  ${
    $d A o v x $.  $d F o v x $.  $d J o v x $.  $d K o v x $.  $d L o v x $.
    $d X o v x $.  $d Y o v x $.
    conttnf.1 $e |- L = ( ( nei ` J ) ` { A } ) $.
    conttnf.2 $e |- X = U. K $.
    conttnf.3 $e |- Y = U. J $.
    $( ` F ` is continous at point ` A ` iff a limit of ` F ` when ` x ` tends
       to ` A ` is ` ( F `` A ) ` .  Bourbaki TG I.50 prop. 9.  (Contributed by
       FL, 29-May-2011.) $)
    conttnf $p |- ( ( J e. Top /\ K e. Top /\ ( A e. Y /\ F : Y --> X ) )
      -> ( F e. ( ( J CnP K ) ` A ) <->
         ( F ` A ) e. ( ( K fLimf L ) ` F ) ) ) $=
      ( vo vv vx ctop wcel wf wa w3a cfv cv cima wss wrex wi wral ccnp co
      cflimf ffvelrn ancoms 3ad2ant3 biantrurd wb iscnp cuni elssuni eqcomi
      sseq2i csn cnei opnneip eleq2i wceq imaeq2 sseq1d rcla4ev ex sylbir a1dd
      syl 3exp 3impd adantr com4l 3expib r19.23aiv com12 com24 3expd sylbi
      mpcom com13 3adant2 pm2.43i imp31 isneip imass2 sstr2 anim2d reximdv a1d
      adantl syl6bi com23 ad2antrr syl5bi r19.23adv impbid pm5.74da ralbidva
      anbi2d bitrd ibar bitr4d 3exp1 imp4a 3imp cfil simp2 c0 wne simp1 snssi
      snnzg neifil syl111anc syl5eqel unieqi unnei syl5reqr mpan9 feq2d biimpd
      imp 3impia eqid isflimf 3bitr4d ) CNOZDNOZAGOZGFBPZQZRZABSZKTZOZBLTZUAZYP
      UBZLEUCZUDZKDUEZYOFOZUUCQZBACDUFUGSOZYOBDEUHUGSOZYNUUDUUCYMYIUUDYJYLYKUUD
      GFABUIUJUKULYIYJYMUUFUUCUMZYIYJYKYLUUHYIYJYKYLUUHYIYJYKRZYLQUUFYLUUCQZUUC
      UUIUUFUUJUMYLUUIUUFYLYQAMTZOZBUUKUAZYPUBZQZMCUCZUDZKDUEZQUUJMKABCDGFJIUNU
      UIUURUUCYLUUIUUQUUBKDUUIYPDOZQZYQUUPUUAUUTYQQZUUPUUAUUIUUSYQUUPUUAUDZUUIU
      USYQUVBUDZUDZYIYKUUIUVDUDYJUUSUUIYIYKQZUVCYPDUOZUBZUUSUUIUVEUVCUDUDZYPDUP
      UVGYPFUBZUUSUVHUDUVFFYPFUVFIUQURUVIUUSUUIUVEUVCUVIUUPYQUUSUUIUVERZUUAUUPU
      VIYQUVJUUAUDUDZUUOUVIUVKUDZMCUUKCOZUULUUNUVLUVJUVMUULUUNRZUVIYQUUAUVEUUSU
      VNUVIUUBUDZUDZUUIYIUVPYKYIUVMUULUUNUVOYIUVMUULUUNUVOUDZYIUVMUULRUUKAUSZCU
      TSSZOZUVQACUUKVAUVTUUNUUBUVIUVTUUNUUAYQUVTUUKEOZUUNUUAUDEUVSUUKHVBUWAUUNU
      UAYTUUNLUUKEYRUUKVCYSUUMYPYRUUKBVDVEVFVGVHVIVIVJVKVLVMUKVNVOVPVQVRVSVTWAW
      BWCWDWEUVAYTUUPLEYREOYRUVSOZUVAYTUUPUDZEUVSYRHVBUVAUWBUWCUDZUUIUVAUWDUDZU
      USYQYIYKUWEYJUVEUWBUVAUWCUVEUWBYRGUBZUULUUKYRUBZQZMCUCZQUVAUWCUDZAMCYRGJW
      FUWIUWJUWFUWIUWCUVAYTUWIUUPYTUWHUUOMCYTUWGUUNUULUWGYTUUNUWGUUMYSUBYTUUNUD
      UUKYRBWGUUMYSYPWHVJVQWIWJVQWKWLWMWNWCWOWDWPWQWRWSWTXAXBVMYLUUCUUJUMUUIYLU
      UCXCWLXDXEXFXGYNYJEXHOEUOZFBPZUUGUUEUMYIYJYMXIYNEUVSXHHYNYIUVRGUBZUVRXJXK
      ZUVSXHOYIYJYMXLYMYIUWMYJYKUWMYLAGXMZVMUKYMYIUWNYJYKUWNYLAGXNVMUKUVRCGJXOX
      PXQYIYJYMUWLYMYIYJQZUWLYKYLUWPUWLUDYKUWPYLUWLYKUWPYLUWLUDYKUWPQZYLUWLUWQG
      UWKFBYKUWMUWPGUWKVCZUWOYIUWMUWRUDYJYIUWMUWRYIUWMQUWKUVSUOGUVSEEUVSHUQXRUV
      RCGJXSXTVGVMYAYBYCVGWNYDVQYEYOKBDEFUWKLIUWKYFYGXPYH $.
      $( [29-May-2011] $)
  $}

  ${
    conttnf2.1 $e |- L = ( ( nei ` J ) ` { A } ) $.
    conttnf2.2 $e |- X = U. K $.
    conttnf2.3 $e |- Y = U. J $.
    $( ` F ` is continous at point ` A ` iff ` ( F `` A ) ` is a limit of the
       image filter of the neighborhoods of ` A ` .  (Contributed by FL,
       7-Aug-2011.) $)
    conttnf2 $p |- ( ( J e. Top /\ K e. Top /\ ( A e. Y /\ F : Y --> X ) )
      -> ( F e. ( ( J CnP K ) ` A ) <->
         ( F ` A ) e. ( ( fLim1 ` K ) ` ( ( X FilMap L ) ` F ) ) ) ) $=
      ( ctop wcel wf wa w3a ccnp co cfv cflimf cfilmap cflim1 conttnf cfil
      cuni wceq simp2 csn cnei wss c0 wne simp1 snssi adantr 3ad2ant3 snnzg
      neifil syl111anc syl5eqel simp3r unieqi unnei syl11anc syl5eq feq2d
      mpbird eqid sflimf eleq2d bitrd ) CKLZDKLZAGLZGFBMZNZOZBACDPQRLABRZBDESQR
      ZLVQBFETQRDUARRZLABCDEFGHIJUBVPVRVSVQVPVLEUCLEUDZFBMZVRVSUEVKVLVOUFVPEAUG
      ZCUHRRZUCHVPVKWBGUIZWBUJUKZWCUCLVKVLVOULZVOVKWDVLVMWDVNAGUMUNUOZVOVKWEVLV
      MWEVNAGUPUNUOWBCGJUQURUSVPWAVNVKVLVMVNUTVPVTGFBVPVTWCUDZGEWCHVAVPVKWDWHGU
      EWFWGWBCGJVBVCVDVEVFBDEFVTIVTVGVHURVIVJ $.
      $( [8-Aug-2011] $) $( [7-Aug-2011] $)
  $}

  ${
    $d F u v x y $.  $d J u v x y $.  $d K u v x y $.  $d P u v x y $.
    $d X u v x y $.  $d Y u v x y $.
    iscnp3.1 $e |- X = U. J $.
    iscnp3.2 $e |- Y = U. K $.
    $( The predicate " ` F ` is a continuous function from topology ` J ` to
       topology ` K ` at point ` P ` ." in terms of neighborhoods.
       (Contributed by FL, 18-Jul-2011.) $)
    iscnp3 $p |- ( ( J e. Top /\ K e. Top /\ P e. X ) ->
      ( F e. ( ( J CnP K ) ` P ) <-> ( F : X --> Y
          /\ A. y e. ( ( nei ` K ) ` { ( F ` P ) } )
          E. x e. ( ( nei ` J ) ` { P } ) ( F " x ) C_ y ) ) ) $=
      ( vv vu ctop wcel w3a ccnp co cfv wf cv cima wss wa wrex wi wral csn
      cnei iscnp neii2 r19.29r fvex snss pm2.27 simp3l1 ad2antrr simprl snssi
      ad2antrl adantl opnneiss syl111anc sstr2 ad2antll com12 3ad2ant1 adantr
      imp jca ex reximdv2 com23 3exp com4r 3imp weq imaeq2 sseq1d cbvrexv
      syl8ib syl6 sylbir imp31 impcom r19.23aiva syl 3ad2antl2 pm2.43i com24
      r19.21aiv opnneip sseq2 rexbidv rcla4va snssg imass2 sstr pm3.2 syl5com
      com3l syl6bir imp4b reximdv com3r 3adant2 r19.23aiv com34 3ad2ant2
      anabsi5 impbida pm5.32da bitrd ) EMNZFMNZCGNZOZDCEFPQRNGHDSZCDRZKTZNZCLTZ
      NZDYAUAZXSUBZUCZLEUDZUEZKFUFZUCXQDATZUAZBTZUBZACUGZEUHRRZUDZBXRUGZFUHRRZU
      FZUCLKCDEFGHIJUIXPXQYHYRXPXQUCZYHYRYSYHUCYOBYQXPXQYHYKYQNZYOUEXPYTYHXQYOX
      PYTYHXQYOUEZUEZXPYTUCZUUBXNXMYTUUCUUBUEZXOXNYTUCYPXSUBZXSYKUBZUCZKFUDZUUD
      YPKFYKUJUUHYHUUCUUAUUHYHUUCUUAUEZUUHYHUCUUGYGUCZKFUDUUIUUGYGKFUKUUJUUIKFU
      UJXSFNZUUIUUEUUFYGUUKUUIUEZUUEXTUUFYGUULUEUEXRXSCDULUMXTYGUUFUULXTYGYFUUF
      UULUEXTYFUNYFUUFUUKUUIYFUUFUUKOUUCXQYCYKUBZLYNUDZYOYFUUFUUKUUCXQUUNUEZUEU
      UFUUKUUCYFUUOUUFUUKUUCYFUUOUEUUFUUKUUCOZXQYFUUNUUPXQYFUUNUEUUPXQUCZYEUUML
      EYNUUQYAENZYEUCZYAYNNZUUMUCUUQUUSUCZUUTUUMUVAXMUURYMYAUBZUUTUUPXMXQUUSXMX
      NXOYTUUFUUKUOUPUUQUURYEUQUUSUVBUUQYBUVBUURYDCYAURUSUTYMEYAVAVBUUQUUSUUMUU
      PUUSUUMUEZXQUUFUUKUVCUUCUUSUUFUUMYDUUFUUMUEUURYBYCXSYKVCVDVEVFVGVHVIVJVKV
      JVLVMVNVOUUMYLLAYNLAVPYCYJYKYAYIDVQVRVSVTVMWAVLWBWCWDWEWFVJVLWFWGWHVJWIWC
      WJYSYRUCYGKFYSYRUUKYGUEYSUUKYRYGYSUUKXTYRYFXPXQUUKXTYRYFUEZUEZUEZXNXMYSUV
      FUEXOXNUUKYSUVEXNUUKXTYSUVDXNUUKXTYSUVDUEZXNUUKXTOXSYQNZUVGXRFXSWKUVHYRYS
      YFUVHYRYSYFUEZUVHYRUCYJXSUBZAYNUDZUVIYOUVKBXSYQBKVPYLUVJAYNYKXSYJWLWMWNUV
      JUVIAYNYSYIYNNZUVJYFXPUVLUVJYFUEZUEZXQXMXOUVNXNXMXOUVNXMUVLXOUVMXMUVLXOUV
      MUEZXMUVLUCUVBYAYIUBZUCZLEUDZUVOYMLEYIUJXOUVJUVRYFXOUVJUVRYFUEXOUVJUCUVQY
      ELEXOUVJUVBUVPYEXOUVBUVJUVPYEUEZXOUVBYBUVJUVSUECYAGWOUVPYBUVJYEUVPYCYJUBZ
      YBUVJYEUEUEYAYIDWPUVTUVJYBYEUVTUVJYBYEUEUVTUVJUCYDYBYEYCYJXSWQYBYDWRWSVJV
      LWFWTXAVLXBXCVJXDWFVJVLVHXEVGWTXFWFVJVLWFVMXGVLXHXIXGVLVHWJXJXKXL $.
      $( [18-Jul-2011] $)
  $}

  ${
    cnfillim4.1 $e |- X = U. J $.
    cnfillim4.2 $e |- Y = U. K $.
    $( If ` G ` is continuous at point ` A ` , and the filter base ` F `
       converges to ` A ` then ` G ( F ) ` converges to ` G ( A ) ` .  Bourbaki
       TG I.50 cor 1.  (Contributed by FL, 19-Sep-2011.) $)
    cnpfillim4 $p |- ( ( ( J e. Top /\ K e. Top /\ F e. fBas ) /\
       ( G e. ( ( J CnP K ) ` A ) /\
       A e. ( ( fLim1 ` J ) ` ( filGen ` F ) ) /\ X = U. F ) ) ->
       ( G ` A ) e. ( ( fLim1 ` K ) ` ( ( Y FilMap F ) ` G ) ) ) $=
      ( ctop wcel cfbas w3a ccnp co cfv cfg cflim1 cuni wceq wa csn cnei
      cfilmap cfil wss simpl2 cvv wf uniexg syl5eqel 3ad2ant2 adantr c0 wne
      simpl1 fgfil 3ad2ant3 wi eqid fgbas eqeq1 syl5ibr com12 imp simpr2
      flimelbas syl31anc simpl snssi adantl snnzg 3jca syl11anc neifil syl
      filfbas simpr1 cnpf snssd unnei feq2d mpbird fmf syl111anc simpl3 simpr3
      eqcomd fmbas eqtrd wb isfillim mpbid simprd filmapss syl33anc fbfgfmeq
      sseqtr4d conttnf2 syl112anc limfilss syl311anc ) DJKZEJKZBLKZMZCADENOPKZA
      BQPZDRPPKZFBSZTZMZUAZXDCGAUBZDUCPPZUDOPZUEKZCGBUDOPZUEKZMGXPSZTGXRSZTXPXR
      UFACPZXPERPZPKZYBXRYCPKXMXDXQXSXCXDXEXLUGZXMGUHKZXOLKZXOSZGCUIZXQXFYFXLXD
      XCYFXEXDGESUHIEJUJUKULUMZXMXOUEKZYGXMXCXNFUFZXNUNUOZMZYKXMXCAFKZYNXCXDXEX
      LUPZXMXCXHUEKZFXHSZTZXIYOYPXFYQXLXEXCYQXDBUQZURUMZXFXLYSXEXCXLYSUSXDXLXEY
      SXKXGXEYSUSXIXEYSXKXJYRTBXJXJUTZVAFXJYRVBVCURVDURVEZXFXGXIXKVFZAXHDFYRHYR
      UTZVGVHZXCYOUAXCYLYMXCYOVIYOYLXCAFVJVKYOYMXCAFVLVKVMVNXNDFHVOVPXOVQVPZXMY
      IFGCUIZXMXCXDYOXGUUHYPYEUUFXFXGXIXKVRZACDEFGHIVSVHZXMYHFGCXMXCYLYHFTYPXMA
      FUUFVTXNDFHWAVNZWBWCZUHXOCGYHYHUTZWDWEXMYFXEXJGCUIZXSYJXCXDXEXLWFZXMUUNUU
      HUUJXMXJFGCXMFXJXFXGXIXKWGWHWBWCZUHBCGXJUUBWDWEVMXMXTGXMYFYGYIXTGTYJUUGUU
      LUHXOCGYHUUMWIWEWHXMYAGXMYFXEUUNYAGTYJUUOUUPUHBCGXJUUBWIWEWHXMXPCGXHUDOPZ
      XRXMYFYGXHLKZYIYHYRTXOXHUFZXPUUQUFYJUUGXFUURXLXEXCUURXDXEYQUURYTXHVQVPURU
      MUULXMYHFYRUUKUUCWJXMYOUUSXMXIYOUUSUAZUUDXMXCYQYSXIUUTWKYPUUAUUCAXHDFYRHU
      UEWLWEWMWNUHXOXHCGYHYRUUMUUEWOWPXMYFXEUUNXRUUQTYJUUOUUPBUHCXHGXJUUBXHUTWQ
      WEWRXMXGYDUUIXMXCXDYOUUHXGYDWKYPYEUUFUUJACDEXOGFXOUTIHWSWTWMYBXPXREGXTYAI
      XTUTYAUTXAXB $.
      $( [19-Sep-2011] $)
  $}


  ${
    $d F x $.  $d J x $.  $d L x $.  $d X x $.
    limfn.1 $e |- X = U. J $.
    limfn.2 $e |- Y = U. L $.
    $( The limits of a function are elements of its range.  (Contributed by FL,
       15-Oct-2012.) $)
    limfn $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) -> ( ( J fLimf L ) `
    F ) C_ X ) $=
      ( vx ctop wcel cfil wf w3a cflimf co cfv cfilmap cflim1 sflimf cv csn
      cnei wss crab cuni wceq simp1 cvv cfbas uniexg syl5eqel 3ad2ant1 filfbas
      3ad2ant2 simp3 fmf syl111anc fmbas eqcomd eqid limfil ssrab2 a1i eqsstrd
      ) BIJZCKJZEDALZMZABCNOPADCQOPZBRPPZDABCDEFGSVHVJHTUABUBPPVIUCZHDUDZDVHVEV
      IKJZDVIUEZUFVJVLUFVEVFVGUGVHDUHJZCUIJZVGVMVEVFVOVGVEDBUEUHFBIUJUKULZVFVEV
      PVGCUMUNZVEVFVGUOZUHCADEGUPUQVHVNDVHVOVPVGVNDUFVQVRVSUHCADEGURUQUSVIBDVNH
      FVNUTVAUQVLDUCVHVKHDVBVCVDVD $.
      $( [15-Oct-2012] $)

    $( If ` A ` is a limit of a function ` F ` , ` A ` is an element of the
       range of ` F ` .  (Contributed by FL, 15-Oct-2012.) $)
    limfn2 $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) -> ( A e. ( ( J
        fLimf L ) `
    F ) -> A e. X ) ) $=
      ( ctop wcel cfil wf w3a cflimf co cfv limfn sseld ) CIJDKJFEBLMBCDNOPEABC
      DEFGHQR $.
      $( [15-Oct-2012] $)

    ${
      limfn3.1 $e |- A = U. ( ( J fLimf L ) ` F ) $.
      $( If ` A ` is the limit of a convergent function in a Hausdorff space,
         ` A ` is an element of the range of the function.  (Contributed by FL,
         15-Oct-2012.) $)
      limfn3 $p |- ( ( J e. Haus /\ L e. Fil /\ F : Y --> X ) -> ( ( ( J fLimf
          L ) `
      F ) =/= (/) -> A e. X ) ) $=
        ( cha wcel cfil wf w3a cflimf co cfv c0 wne wa ctop wss haustop
        3ad2ant1 adantr simpl2 simpl3 limfn syl111anc limvinlv sseldd ex ) CJKZ
        DLKZFEBMZNZBCDOPQZRSZAEKUPURTZUQEAUSCUAKZUNUOUQEUBUPUTURUMUNUTUOCUCUDUE
        UMUNUOURUFUMUNUOURUGBCDEFGHUHUIABCDEFGHIUJUKUL $.
        $( [15-Oct-2012] $)
    $}
  $}

  ${
    $d A o o' s $.  $d F o o' s $.  $d G o o' s $.  $d J o o' s $.
    $d K o o' s $.  $d L o o' s $.  $d X o o' s $.  $d Y o o' s $.
    cmptdst.1 $e |- Y = U. J $.
    cmptdst.2 $e |- X = U. L $.
    $( ` G o. F ` tends to ` G ( A ) ` if ` G ` is continuous at point ` A `
       and ` F ` tends to A .  Bourbaki TG I.50 cor. 2.  (Contributed by FL,
       15-Oct-2012.) $)
    cmptdst $p |- ( ( ( J e. Top /\ K e. Top /\ L e. Fil ) /\ ( F : X
    --> Y /\ A e. ( ( J fLimf L ) ` F ) /\ G e. ( ( J CnP K ) ` A ) ) )
    -> ( G ` A ) e. ( ( K fLimf L ) ` ( G o. F ) ) ) $=
      ( vo vs bnjvom ctop wcel cfil w3a wf cflimf co cfv ccnp wa ccom cuni cv
      cima wss wrex wi wral wb simpl2 simpl3 simpl1 wceq feq3 ax-mp biimpi
      3ad2ant1 adantl simpr2 eqid flimfelbas syl31anc syl6eleqr simpr3 cnpf
      simpr1 fco syl11anc isflimf syl111anc ffvelrn ccnv iscnp2 biimpd ra4
      impcom r19.29r pm3.35 sstr imaco wfun cdm ffun 3ad2ant2 adantr fdm
      elssuni sseq2 wfn crn df-f imassrn sstr2 sylbi a1d imbi2d syl5ibr com3r
      syl6bi eqcoms mpsyl com3l syl com12 3adant1 imp funimass3 bicomd biimpcd
      anabsi5 syl5eqss 3exp1 3exp ex com4l 3imp com24 com23 reximdvai 3expd
      imp31 r19.23aiv com5l com13 syl9 syl6 pm2.43b 3ad2ant3 anabsi7 sylbid
      mpd r19.21aiv mpbir2and ) DNOZENOZFPOZQZGHBRZABDFSTUAOZCADEUBTUAOZQZUCZAC
      UAZCBUDZEFSTUAOZUUFEUEZOZUUFKUFZOZUUGLUFZUGZUUKUHZLFUIZUJZKEUKZUUEYRYSGUU
      IUUGRZUUHUUJUURUCULYQYRYSUUDUMZYQYRYSUUDUNZUUEHUUICRZUUAUUSUUEYQYRAHOZUUC
      UVBYQYRYSUUDUOZUUTUUEADUEZHUUEYQYSGUVEBRZUUBAUVEOZUVDUVAUUDUVFYTUUAUUBUVF
      UUCUUAUVFHUVEUPUUAUVFULIHUVEGBUQURUSUTVAYTUUAUUBUUCVBZABDFUVEGUVEVCZJVDVE
      ZIVFZYTUUAUUBUUCVGZACDEHUUIIUUIVCZVHVEYTUUAUUBUUCVIZGHUUICBVJVKUUFKUUGEFU
      UIGLUVMJVLVMUUEUVEUUICRZUVGUUJUUEYQYRUVGUUCUVOUVDUUTUVJUVLACDEUVEUUIUVIUV
      MVHVEUVJUVEUUIACVNVKUUEUUQKEUUEUUBUUKEOZUUQUJZUVHUUEUUBUVCAMUFZOZBUUMUGZU
      VRUHZLFUIZUJZMDUKZUCZUVQUUEYQYSUUAUUBUWEULUVDUVAUVNAMBDFHGLIJVLVMYTUUDUWE
      UVQUJZUUCUUAUUEUWFUJZUUBUUCUUEUWFUUEUUCUVBUULUVSUVRCVOUUKUGZUHZUCZMDUIZUJ
      ZKEUKZUCZUWGUUEYQYRUVCUUCUWNUJUVDUUTUVKYQYRUVCQUUCUWNMKACDEHUUIIUVMVPVQVM
      UWMUVBUWGUWMUVBUUEUWEUVQUVPUWMUVBUUEUWEQZUUQUVPUWMUWOUUQUJUVPUWMUCUULUWKU
      WOUUPUWMUVPUWLUWLKEVRVSUVBUUEUWEUWKUUPUJZUWEUUEUVBUWPUWDUVCUUEUVBUWPUJUJU
      WKUWDUVCUUEUVBUUPUWKUWDUVCUUEUVBUUPUJUJZUJZUWKUWDUCUWJUWCUCZMDUIUWRUWJUWC
      MDVTUWSUWRMDUWSUVRDOZUWRUVSUWIUWCUWTUWRUJZUVSUWCUWIUXAUVSUWCUWIUXAUJZUVSU
      WCUCUWBUXBUVSUWBWAUWBUWIUWTUVCUWQUWBUWIUWTUVCQZUUEUVBUUPUXCUUEUVBQZUWBUUP
      UXDUWAUUOLFUXDUWAUUMFOZUUOUXCUUEUVBUWAUXEUUOUJZUJUXCUWAUVBUUEUXFUWIUWTUVC
      UWAUVBUUEUXFUJUJZUJUWAUWIUWTUVCUXGUWAUWIUWTUVCUXGUJUJZUWAUWIUCUVTUWHUHZUX
      HUVTUVRUWHWBUXIUWTUVCUXGUXIUWTUVCQZUVBUUEUXEUUOUXJUVBUUEQZUXEUCZUUNCUVTUG
      ZUUKCBUUMWCUXKUXEUXMUUKUHZUXJUVBUXLUXNUJZUUEUXIUWTUXOUVCUXLUXIUXNUXLCWDZU
      VTCWEZUHZUXIUXNULUXKUXPUXEUVBUXJUXPUUEHUUICWFWGWHUXKUXEUXRUVBUUEUXEUXRUJZ
      UXJUUEUVBUXSUUDUVBUXSUJZYTUUAUUBUXTUUCUVBUUAUXSUVBUXQHUPZUUAUXSUJHUUICWIU
      XEUYAUUAUXRGFUEZUPUXEUUMUYBUHZUYAUUAUXRUJUJZJUUMFWJUYCUYDUJUYBGUYBGUPUYCU
      UMGUHZUYDUYBGUUMWKUYAUUAUYEUXRUUAUYEUXRUJUYAUYEUVTHUHZUJUUAUYFUYEUUABGWLZ
      BWMZHUHZUCUYFGHBWNUYIUYFUYGUVTUYHUHUYIUYFUJBUUMWOUVTUYHHWPURVAWQWRUYAUXRU
      YFUYEUXQHUVTWKWSWTXAXBXCXDXEXFXGUTVAVSXHXIUXPUXRUCUXNUXIUVTUUKCXJXKVKXLUT
      UTXMXNXOXPXFXQXRXSXTXSYAYBXGYCYCXFXQYAYDXGYEXFXQYFVSYGXSYHXQXEYCVSYIYJYKY
      LYMYNYOYP $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( If ` A ` is an element of ` B ` and ` B ` has a unique element,
       ` U. B = A ` .  (Contributed by FL, 15-Oct-2012.) $)
    unexun $p |- ( ( A e. B /\ E! x x e. B ) -> U. B = A ) $=
      ( wcel cv weu wa cuni cab abid2 eqcomi unieqi wceq simpl eleq1 euuni2
      mpbid syl5eq ) BCDZAEZCDZAFZGZCHUAAIZHZBCUDUDCACJKLUCSUEBMSUBNUASABCTBCOP
      QR $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d F x $.  $d J x $.  $d L x $.  $d X x $.  $d Y x $.  $d A x $.
    limhun.1 $e |- X = U. J $.
    limhun.2 $e |- Y = U. L $.
    limhun.3 $e |- L1 = U. ( ( J fLimf L ) ` F ) $.
    $( In a Hausdorff space if ` A ` is a limit of a convergent function
       ` F ` , then ` A ` is the unique limit of ` F ` .  (Contributed by FL,
       15-Oct-2012.) $)
    limhun $p |- ( ( ( J e. Haus /\ L e. Fil /\ F : Y --> X ) /\
         A e. ( ( J fLimf L ) ` F ) )
         -> L1 = A ) $=
      ( vx cha wcel cfil wf w3a cflimf co cfv wceq wi c0 wne ne0i wa cv weu
      holimf2 cuni unexun syl5eq ex syl5com com3l mpcom com12 3com12 imp ) CLMZ
      DNMZFEBOZPABCDQRSZMZGATZUTUSVAVCVDUAZVCUTUSVAPZVDVBUBUCZVCVFVDUAVBAUDVFVG
      VCVDVFVGVEVFVGUEKUFVBMKUGZVCVDKBCDEFIHUHVCVHVDVCVHUEGVBUIAJKAVBUJUKULUMUL
      UNUOUPUQUR $.
      $( [15-Oct-2012] $)
  $}

  ${
    cmptdst2.1 $e |- Y = U. J $.
    cmptdst2.2 $e |- X = U. L $.
    cmptdst2.3 $e |- L1 = U. ( ( J fLimf L ) ` F ) $.
    cmptdst2.4 $e |- L2 = U. ( ( K fLimf L ) ` ( G o. F ) ) $.
    $( ` G o. F ` tends to ` G ( L1 ) ` if ` G ` is continuous at point ` L1 `
       and ` F ` tends to ` L1 ` .  ( ~ cmptdst in the Hausdorff case.)
       Bourbaki TG I.50 cor. 2.  (Contributed by FL, 15-Oct-2012.) $)
    cmptdst2 $p |- ( ( ( J e. Haus /\ K e. Haus /\ L e. Fil ) /\ ( F : X
    --> Y /\ ( ( J fLimf L ) ` F ) =/= (/) /\ G e. ( ( J CnP K ) ` L1 ) ) )
    -> L2 = ( G ` L1 ) ) $=
      ( cha wcel cfil w3a wf cflimf co cfv c0 wne ccnp wa cuni ccom wceq
      simpl2 simpl3 ctop haustop 3ad2ant1 adantr 3ad2ant2 simpl1 feq2i biimpi
      adantl 3jca simpr2 eqid limfn3 sylc simpr3 cnpf syl31anc simpr1 fco
      syl11anc id 3anim123i limvinlv cmptdst syl13anc limhun ) CNOZDNOZEPOZQZFG
      ARZACESTUAZUBUCZBHCDUDTUAOZQZUEZVRVSFDUFZBAUGZRZHBUAZWHDESTUAOZIWJUHVQVRV
      SWEUIVQVRVSWEUJZWFGWGBRZWAWIWFCUKOZDUKOZHGOZWDWMVTWNWEVQVRWNVSCULZUMUNVTW
      OWEVRVQWOVSDULZUOUNWFVQVSEUFZGARZQWCWPWFVQVSWTVQVRVSWEUPZWLWEWTVTWAWCWTWD
      WAWTFWSGAKUQURUMUSUTVTWAWCWDVAZHACEGWSJWSVBLVCVDVTWAWCWDVEZHBCDGWGJWGVBZV
      FVGVTWAWCWDVHZFGWGBAVIVJWFWNWOVSQZWAHWBOZWDWKVTXFWEVQWNVRWOVSVSWQWRVSVKVL
      UNXEWFVQVSWAWCXGXAWLXEXBHACEGFJKLVMVGXCHABCDEFGJKVNVOWJWHDEWGFIXDKMVPVG
      $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d A o o1 u v x $.  $d B o o1 u v x $.  $d C o o1 u v $.  $d D o o1 u v $.
    $d N o o1 u v $.  $d R o o1 u v x $.  $d R o' o1 u v x $.
    $d S o o1 u v x $.  $d S o' o1 u v x $.
    exopcopn.1 $e |- T = ( R tX S ) $.
    $( For every neighborhood ` N ` of ` <. A , B >. ` in a product topology,
       there exist two open sets ` u ` and ` v ` of the component topologies so
       that ` ( u X. v ) ` is an open neighborhood of ` <. A , B >. ` and a
       part of ` N ` .  (Use ~ opelxpg to have ` A e. u ` and ` B e. v ` .)
       (Contributed by FL, 15-Oct-2012.) $)
    exopcopn $p |- ( ( R e. Top /\ S e. Top /\ ( A e. C /\ B e. D ) ) ->
      ( N e. ( ( nei ` T ) ` { <. A , B >. } ) -> E. u e. R E. v e. S
          ( <. A , B >. e. ( u X. v ) /\ ( u X. v ) C_ N ) ) ) $=
      ( vo bnjvom vx bnjvo1 ctx co wceq ctop wcel wa w3a cop csn cnei cfv cv
      cxp wss wrex wi cuni eqid txuni 3adant3 adantr txtop neii1 expcom adantl
      wb sseq2 eqcoms imbi2d syl5ibrcom 3imp cvv opex elnei 3exp com3l ax-mp
      imp 3adant2 jca ssel2 simpl3 simp2 eqcomd eleq2d biimpa isneip syl11anc
      cab ctg 3simpa 3ad2ant1 txval syl ctb txbas wel wral eltg2 eleq1 anbi1d
      rexbidv rcla4va visset weq eqeq1 elab sstr eleq2 sseq1 anbi12d biimpcd
      ex syl5com com3r imp31 3adant1 reximdv com14 sylbi r19.23aiv com23
      syl6bi 3syl pm2.43i sylbid pm2.43b exp3a 3expd impcom com12 mpd3an23
      fveq2 fveq1d imbi1d syl5ibr ) IGHPQZRZGSTZHSTZCETDFTUAZUBZJCDUCZUDZIUEUFZ
      UFZTZYRBUGAUGUHZTZUUCJUIZUAZAHUJZBGUJZUKZUKKYQUUIYMJYSYLUEUFZUFZTZUUHUKYQ
      UULUUHYQUULUAZYLULZGULZHULZUHZRZYLSTZUUHYQUURUULYNYOUURYPGHYLUUOUUPYLUMZU
      UOUMUUPUMUNUOUPYQUUSUULYNYOUUSYPGHYLUUTUQUOUPUUMUURUUSUBZUUHUVAJUUQUIZYRJ
      TZUAYRUUQTZUVAUUHUKUVAUVBUVCUUMUURUUSUVBUUMUUSUVBUKUURUUSJUUNUIZUKZUULUVF
      YQUUSUULUVEYSYLJUUNUUNUMZURUSUTUURUVBUVEUUSUVBUVEVAUUQUUNUUQUUNJVBVCVDVEV
      FUUMUUSUVCUURUUMUUSUVCUULUUSUVCUKZYQYRVGTZUULUVHUKCDVHUUSUVIUULUVCUUSUVIU
      ULUVCVGYRYLJVIVJVKVLUTVMVNVOJUUQYRVPUVAUVDUUHUUMUURUUSUVDUUHUKZUULYQUURUU
      SUVJUKUKUULYQUURUUSUVJUULYQUURUUSUBZUVDUUHUULUVKUVDUAZUUHUVLUULUVEYRLUGZT
      ZUVMJUIZUAZLYLUJZUAZUVLUUHUKZUVLUUSYRUUNTZUULUVRVAYQUURUUSUVDVQUVKUVDUVTU
      VKUUQUUNYRUVKUUNUUQYQUURUUSVRVSVTWAYRLYLJUUNUVGWBWCUVQUVSUVEUVPUVSLYLUVLU
      VMYLTZUVPUUHUVLUWAUVMMUGZUUCRZAHUJZBGUJZMWDZWEUFZTZUVPUUHUKZUVLYNYOUAZUWA
      UWHVAUVKUWJUVDYQUURUWJUUSYNYOYPWFWGUPZUWJYLUWGUVMMABGHWHVTWIUVLUWHUWIUKZU
      VLUWJUWFWJTZUVLUWLUKUWKMABGHWKUWMUWHUVLUWIUWMUWHUVMUWFULUIZNOWLZOUGZUVMUI
      ZUAZOUWFUJZNUVMWMZUAUVLUWIUKZNOUVMUWFWNUWTUXAUWNUVPUWTUVLUUHUVNUVOUWTUVSU
      KUVNUWTUVOUVSUVNUWTUVOUVSUKZUVNUWTUAYRUWPTZUWQUAZOUWFUJZUXBUWSUXENYRUVMNU
      GZYRRZUWRUXDOUWFUXGUWOUXCUWQUXFYRUWPWOWPWQWRUXDUXBOUWFUWPUWFTUWPUUCRZAHUJ
      ZBGUJZUXDUXBUKUWEUXJMUWPOWSMOWTZUWDUXIBGUXKUWCUXHAHUWBUWPUUCXAWQWQXBUVLUX
      DUVOUXJUUHUVLUXDUVOUXJUUHUKUVLUXDUVOUBZUXIUUGBGUXLUXHUUFAHUXDUVOUXHUUFUKZ
      UVLUXCUWQUVOUXMUWQUVOUXCUXMUWQUVOUXCUXMUKUWQUVOUAUWPJUIZUXCUXMUWPUVMJXCUX
      CUXNUXMUXHUXCUXNUAUUFUXHUXCUUDUXNUUEUWPUUCYRXDUWPUUCJXEXFXGXHXIXHXJXKXLXM
      XMVJXNXOXPWIXHXQVMVKUTXRXQXSXTYAVKXPUTXRYBYCYDYEVFYFXSXTYGXHYMUUBUULUUHYM
      UUAUUKJYMYSYTUUJIYLUEYHYIVTYJYKVL $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d A o p q $.  $d B o p q $.  $d R p q $.  $d S p q $.  $d T o p q $.
    $d U o p q $.  $d V o p q $.  $d X p q $.  $d Y p q $.
    prdnei.1 $e |- T = ( R tX S ) $.
    prdnei.2 $e |- X = U. R $.
    prdnei.3 $e |- Y = U. S $.
    $( The product of two neighborhoods is a neighborhood.  (Contributed by FL,
       15-Oct-2012.) $)
    prdnei $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. X /\ B e. Y ) /\ ( U e.
        ( ( nei ` R ) ` { A } )
    /\ V e. ( ( nei ` S ) ` { B } ) ) ) -> ( U X. V ) e. ( ( nei ` T ) ` { <. A
        , B >. } ) ) $=
      ( vo vp vq ctop wcel wa csn cnei cfv w3a cxp cop cuni wss cv wrex wb
      txtop 3ad2ant1 opelxpi 3ad2ant2 wceq txuni eleqtrrd eqid isneip syl11anc
      wi simp1 simpr1l simpr3l neii1 simpr1r simpr3r xpss12 sseq2 adantr
      mpbird ex 3syl pm2.43i ad2ant2r ad2ant2l anbi12d biimp3a simp31 simp12
      simp111 jca32 txopn simp13l simp12l simp13r simp12r eleq2 sseq1 rcla4ev
      expcom com12 3exp com4l r19.23aiv impcom mpcom mpbir2and ) CPQZDPQZRZAHQZ
      BIQZRZFASZCTUAUAQZGBSZDTUAUAQZRZUBZFGUCZABUDZSETUAUAQZXJEUEZUFZXKMUGZQZXO
      XJUFZRZMEUHZXIEPQZXKXMQXLXNXSRUIWTXCXTXHCDEJUJUKXIXKHIUCZXMXCWTXKYAQXHABH
      IULUMWTXCXMYAUNZXHCDEHIJKLUOZUKUPXKMEXJXMXMUQURUSXIXNXIWTYBXIXNUTWTXCXHVA
      YCYBXIXNYBXIRZXNXJYAUFZYDFHUFZGIUFZYEYDWRXEYFWRWSXCXHYBVBXEXGWTXCYBVCXDCF
      HKVDUSYDWSXGYGWRWSXCXHYBVEXEXGWTXCYBVFXFDGILVDUSFHGIVGUSYBXNYEUIXIXMYAXJV
      HVIVJVKVLVMYFANUGZQZYHFUFZRZNCUHZRZYGBOUGZQZYNGUFZRZODUHZRZRZXIXSWTXCXHYT
      WTXCRXEYMXGYSWRXAXEYMUIWSXBANCFHKURVNWSXBXGYSUIWRXABODGILURVOVPVQYSYMXIXS
      UTZYRYGYMUUAUTZYQYGUUBUTODYNDQZYQYGUUBYMUUCYQYGUBZUUAYLYFUUDUUAUTZYKYFUUE
      UTNCUUDYHCQZYKYFUUAUUDUUFYKYFUUAUTUUDUUFYKUBZYFXIXSUUGYFXIUBZXSUUHWTUUFUU
      CRRYHYNUCZEQZUUHXSUTUUHWTUUFUUCUUGYFWTXCXHVRUUDUUFYKYFXIVSUUCYQYGUUFYKYFX
      IVTWAYHYNCDEJWBUUHUUJXSUUHXKUUIQZUUIXJUFZUUJXSUTUUHYIYOUUKYIYJUUDUUFYFXIW
      CUUGYFYOXIYOYPUUCYGUUFYKWDUKABYHYNULUSUUHYJYPUULYIYJUUDUUFYFXIWEUUGYFYPXI
      YOYPUUCYGUUFYKWFUKYHFYNGVGUSUUJUUKUULRZXSXRUUMMUUIEXOUUIUNXPUUKXQUULXOUUI
      XKWGXOUUIXJWHVPWIWJUSWKVLVMWLWLWMWNWOWKWLWNWOWOWPWQ $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d A s v" v' $.  $d F s v v" v' $.  $d L s v v" v' $.  $d L1 s v v" v' $.
    $d L2 s v v" v' $.  $d R s v" v' $.  $d S s v" v' $.  $d T s v v" v' $.
    $d W s v v" v' $.  $d X s v" v' $.  $d Y s v" v' $.  $d Z s v" v' $.
    limptlimpr2.1 $e |- T = ( R tX S ) $.
    limptlimpr2.2 $e |- W = U. L $.
    limptlimpr2.3 $e |- X = U. R $.
    limptlimpr2.4 $e |- Y = U. S $.
    limptlimpr2.5 $e |- Z = ( X X. Y ) $.
    $( Lemma for ~ limptlimpr . $)
    limptlimpr2lem1 $p |- ( ( ( R e. Top /\ S e. Top /\ L e. Fil ) /\
      ( F : W --> Z /\ L2 e. A ) )
         -> ( <. L1 , L2 >. e. ( ( T fLimf L ) ` F )
         -> ( L1 e. ( ( R fLimf L ) ` ( 1st o. F ) )
               /\ L2 e. ( ( S fLimf L ) ` ( 2nd o. F ) ) ) ) ) $=
      ( vs vv bnjvvm bnjvvn cop cflimf co cfv wcel ctop cfil w3a wf wa c1st
      ccom c2nd cuni cv cima wss wrex csn cnei wral wi wb txtop 3adant3 adantr
      simpl3 wceq cxp txuni syl6reqr feq3 syl biimpcd impcom eqid flimfnei
      syl111anc eleq2 opelxpg syl6bi com23 com12 adantl imp simpl wex simprl1
      simpll simprl2 simplr jca jca31 apnei anim12i ad2antlr simplrl simpr
      3jca prdnei sseq2 rexbidva rcla4va imaco c0 wne simp2l2 3ad2ant1 simp2rr
      simp12 npmp simp3 prjpacp1 syl11anc syl5eqss 3exp reximdvai syl5com ex
      com3l 3expd 3syl pm2.43i r19.21aiv 19.23aiv 3simpa ad2antrl rexbidv
      rcla4cva ax-17 prjpacp2 adantrd 3adant1 a1d reximdai 3exp1 mpdd anim12ii
      ancoms an4 sylanbrc simprl3 ax-mp biimpi ad2antll 1stcof 2ndcof anbi12d
      mpbird mpancom pm2.43a ) KLUBZEDFUCUDUEUFZBUGUFZCUGUFZFUHUFZUIZGJEUJZLAUF
      ZUKZUKZKULEUMZBFUCUDUEUFZLUNEUMZCFUCUDUEUFZUKZUVBUUNUUMDUOZUFZERUPZUQZSUP
      ZURZRFUSZSUUMUTDVAUEUEZVBZUKZUVBUVGVCUVBDUGUFZUUQGUVHEUJZUUNUVQVDUURUVRUV
      AUUOUUPUVRUUQBCDMVEVFVGUUOUUPUUQUVAVHUVAUURUVSUUSUURUVSVCUUTUURUUSUVSUUOU
      UPUUSUVSVDZUUQUUOUUPUKZJUVHVIUVTUWAUVHHIVJZJBCDHIMOPVKZQVLJUVHGEVMVNVFVOV
      GVPUUMSEDFUVHGRUVHVQNVRVSUVQUVBUVGKHUFZLIUFZUKZUVQUVBUKZUVGUVQUVBUWFUVIUV
      BUWFVCUVPUVBUVIUWFUVAUURUVIUWFVCZUUTUURUWHVCUUSUURUUTUWHUUOUUPUUTUWHVCUUQ
      UWAUVIUUTUWFUWAUVHUWBVIZUVIUUTUWFVCZVCUWCUWIUVIUUMUWBUFZUWJUVHUWBUUMVTUUT
      UWKUWFKLHIAWAVOWBVNWCVFWDWEVPWDVGWFUWFUWGUKZUVGUWDUVCUVJUQZTUPZURZRFUSZTK
      UTBVAUEUEZVBZUKZUWEUVEUVJUQZUAUPZURZRFUSZUALUTCVAUEUEZVBZUKZUKZUWLUWFUWRU
      XEUKZUXGUWFUWGWGUWLUXHUWLUUOUWDUKZUUPUWEUKZUKUWNUWQUFZTWHZUXAUXDUFZUAWHZU
      KUWLUXHVCZUWLUUOUWDUXJUWGUUOUWFUUOUUPUUQUVAUVQWIWEZUWDUWEUWGWJUWLUUPUWEUW
      GUUPUWFUUOUUPUUQUVAUVQWKWEZUWDUWEUWGWLWMWNUXIUXLUXJUXNTKBHOWOUALCIPWOWPUX
      NUXLUXOUXNUWLUWRUXLUXEUXMUWLUWRVCUAUXMUWLUWRUXMUWLUKZUWPTUWQUXRUXKUWPUXRU
      XKUKZUWPUXSUWAUWFUXKUXMUKZUIZUWNUXAVJZUVOUFZUXSUWPVCUXSUWAUWFUXTUXSUUOUUP
      UWLUUOUXMUXKUXPWQUWLUUPUXMUXKUXQWQWMUXMUWFUWGUXKWRUXSUXKUXMUXRUXKWSUXMUWL
      UXKWJWMWTKLBCDUWNUXAHIMOPXAZUXSUYCUWPUXRUXKUYCUWPVCZUWLUXMUXKUYEVCZUWGUWF
      UXMUYFVCZUVQUVBUWFUYGVCZUVPUVIUVBUYHVCUVPUVIUVBUWFUYGUVPUVIUVBUWFUIZUXMUX
      KUYEUYCUVPUYIUXMUXKUIZUWPUYCUVPUYJUWPVCUYCUVPUKUVKUYBURZRFUSZUYJUWPUVNUYL
      SUYBUVOUVLUYBVIZUVMUYKRFUYMUVMUYKVDUVJFUFZUVLUYBUVKXBZVGXCXDUYJUYKUWORFUY
      JUYNUYKUWOUYJUYNUYKUIZUWMULUVKUQZUWNULEUVJXEUYPUXAXFXGZUYKUYQUWNURUYPUUPU
      UTUXMUYRUYJUYNUUPUYKUYIUXMUUPUXKUUOUUPUUQUVAUVIUWFXHXIXIUYJUYNUUTUYKUYIUX
      MUUTUXKUUSUUTUURUVIUWFXJXIXIUYIUXMUXKUYNUYKXKLCUXAAXLVSUYJUYNUYKXMUWNUXAU
      VKXNXOXPXQXRXSXTYAYBYBVPWFVPVPWFWDYCYDXTYEXTYFUXKUWLUXEVCTUXKUWLUXEUXKUWL
      UKZUXCUAUXDUYSUXMUXCUYSUXMUKZUXCUYTUYAUYCUYTUXCVCUYTUWAUWFUXTUWLUWAUXKUXM
      UWGUWAUWFUURUWAUVQUVAUUOUUPUUQYGYHWEWQUXKUWFUWGUXMWRUYTUXKUXMUXKUWLUXMWJU
      YSUXMWSWMWTUYDUYCUYTUYLUXCUYTUYCUYLUWLUYCUYLVCZUXKUXMUWGVUAUWFUVPVUAUVIUV
      BUVPUYCUYLUVNUYLSUYBUVOUYMUVMUYKRFUYOYIYJXTWQWEWQWDUYTUYCUYLUXCVCZUYSUXMU
      YCVUBVCZUWLUXKUXMVUCVCZUWGUWFUXKVUDVCZUVQUVBUWFVUEVCZUVIUVBVUFVCUVPUVIUVB
      UWFVUEUYIUXKUXMUYCVUBUYIUXKUXMUIZUYCUKZUYKUXBRFVUHRYKVUHUYKUXBVCZUYNVUGVU
      IUYCUYIUXKVUIUXMUYIUXKVUIUVBUWFUXKVUIVCZUVIUVBUWFVUJUURUWFVUJVCZUVAUUOUUP
      VUKUUQUUOUWDVUJUWEUUOUWDUXKVUIUUOUWDUXKUIUWNXFXGZVUIKBUWNHXLVULUYKUXBVULU
      YKUKUWTUNUVKUQUXAUNEUVJXEUWNUXAUVKYLXPXTVNXQYMXIVGWFYNWFVFVGYOYPYQXQVGWFV
      PVPWFWDYRYCYDXTYEXTYFYSYTYCYDUWDUWRUWEUXEUUAUUBUWLUVDUWSUVFUXFUWLUUOUUQGH
      UVCUJZUVDUWSVDUXPUWGUUQUWFUUOUUPUUQUVAUVQUUCWEZUWLGUWBEUJZVUMUVBVUOUWFUVQ
      UUSVUOUURUUTUUSVUOJUWBVIUUSVUOVDQJUWBGEVMUUDUUEYHUUFZGHIEUUGVNKTUVCBFHGRO
      NVRVSUWLUUPUUQGIUVEUJZUVFUXFVDUXQVUNUWLVUOVUQVUPGHIEUUHVNLUAUVECFIGRPNVRV
      SUUIUUJUUKXTWBUUL $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d F o" o' s s" s' v $.  $d F o" o' s" v v" $.  $d F o" o' s' v v' $.
    $d L o" o' s s" s' v $.  $d L o" o' s" v v" $.  $d L o" o' s' v v' $.
    $d L1 o" o' v v' $.  $d L2 o" o' v v" $.  $d R o" o' s" s' v $.
    $d R o" o' s' v v' $.  $d S o" o' s" s' v $.  $d S o" o' s" v v" $.
    $d T s v $.  $d W o" o' s s" s' v $.  $d W o" o' s" v v" $.
    $d W o" o' s' v v' $.  $d X o" o' s' v v' $.  $d Y o" o' s" v v" $.
    $d Z o" o' s" s' v $.
    limptlimpr2lem2.1 $e |- T = ( R tX S ) $.
    limptlimpr2lem2.2 $e |- W = U. L $.
    limptlimpr2lem2.3 $e |- X = U. R $.
    limptlimpr2lem2.4 $e |- Y = U. S $.
    limptlimpr2lem2.5 $e |- Z = ( X X. Y ) $.
    $( Lemma for ~ limptlimpr . $)
    limptlimpr2lem2 $p |- ( ( ( R e. Top /\ S e. Top /\ L e. Fil ) /\
      F : W --> Z )
         -> ( ( L1 e. ( ( R fLimf L ) ` ( 1st o. F ) )
               /\ L2 e. ( ( S fLimf L ) ` ( 2nd o. F ) ) )
             -> <. L1 , L2 >. e. ( ( T fLimf L ) ` F ) ) ) $=
      ( vs vv bnjvsm bnjvvm bnjvsn bnjvvn bnjvom bnjvon ctop wcel cfil w3a wf
      wa c1st ccom cflimf co cfv c2nd cop cuni cv cima wss wrex csn cnei wral
      wi wb simpl1 simpl3 cxp wceq feq3 ax-mp biimpi adantl 1stcof syl
      flimfnei syl111anc simpl2 a1i biimpa 2ndcof anbi12d txuni opelxpi expcom
      eleq2 imbi2d syl5ibr 3adant3 adantr com3l com12 imp31 simprl1 simprl2
      simplll simplrl exopcopn syl112anc opelxpg biimpd an6 opnneip weq sseq2
      rexbidv rcla4va ex im2anan9 cin filint inss1 imass2 inss2 sstr imaco
      sseq1i anbi12i xpss12 wrel crn imassrn frn relxp relss syl6mpi mpsyl
      sylancr relinccppr sstr2 3syl syl5com com23 com24 3imp imaeq2 sseq1d
      rcla4ev syl9r 3expd 3exp com34 3ad2ant3 anabsi5 com5l r19.23aiv imp
      adantld syl6 ad2ant2l syl2an sylbir com25 ad2antrl pm2.43i syl9 ad2antlr
      com4l r19.23aivv pm2.43a r19.21aiv jca syl6bi txtop ad2antrr simpll3
      syl6reqr eqid mpbird ) AUEUFZBUEUFZEUGUFZUHZFIDUIZUJZJUKDULZAEUMUNUOUFZKU
      PDULZBEUMUNUOUFZUJZJKUQZDCEUMUNUOUFZUVQUWBUJZUWDUWCCURZUFZDQUSZUTZRUSZVAZ
      QEVBZRUWCVCCVDUOUOZVEZUJZUVQUWBUWOUWBUVQUWOUVQUWBJGUFZUVRSUSZUTZTUSZVAZSE
      VBZTJVCAVDUOUOZVEZUJZKHUFZUVTUAUSZUTZUBUSZVAZUAEVBZUBKVCBVDUOUOZVEZUJZUJZ
      UVQUWOVFUVQUVSUXDUWAUXMUVQUVLUVNFGUVRUIZUVSUXDVGUVLUVMUVNUVPVHUVLUVMUVNUV
      PVIZUVQFGHVJZDUIZUXOUVPUXRUVOUVPUXRIUXQVKZUVPUXRVGZPIUXQFDVLVMZVNVOFGHDVP
      VQJTUVRAEGFSNMVRVSUVQUVMUVNFHUVTUIZUWAUXMVGUVLUVMUVNUVPVTUXPUVQUXRUYBUVOU
      VPUXRUXTUVOUYAWAWBFGHDWCVQKUBUVTBEHFUAOMVRVSWDUXNUVQUWOUXNUVQUJZUWGUWNUXD
      UXMUVQUWGUWPUXMUVQUWGVFZVFUXCUXMUWPUYDUXEUWPUYDVFUXLUVQUXEUWPUWGUVOUXEUWP
      UWGVFZVFZUVPUVLUVMUYFUVNUVLUVMUJZUWFUXQVKZUYFABCGHLNOWEZUXEUYEUYHUWPUWCUX
      QUFZVFUWPUXEUYJJKGHWFWGUYHUWGUYJUWPUWFUXQUWCWHWIWJVQWKWLWMWLWNWLWOUYCUWLR
      UWMUWJUWMUFZUYCUWLUYCUYKUWCUCUSZUDUSZVJZUFZUYNUWJVAZUJZUDBVBUCAVBZUYCUWLV
      FZUYCUVLUVMUWPUXEUYKUYRVFUVLUVMUVNUVPUXNWPUVLUVMUVNUVPUXNWQUWPUXCUXMUVQWR
      UXDUXEUXLUVQWSUDUCJKGHABCUWJLWTXAUYQUYSUCUDABUYQUYLAUFZUYMBUFZUJZUYSUYOUY
      PVUBUYSVFUYCUYOUYPVUBUWLUYCUYOUYPVUBUWLVFVFZVFZUXMUYCVUDVFZUXDUVQUXEVUEUX
      LUXEUYOJUYLUFZKUYMUFZUJZUYCVUCUXEUYOVUHJKUYLUYMHXBXCUYCVUHVUCVFZUVOUYCVUI
      VFZUXNUVPUVLUVMVUJUVNUYGVUBVUHUYPUYCUWLUYGVUBVUHUYPUYSVFZUYGVUBVUHUHUVLUY
      TVUFUHZUVMVUAVUGUHZUJVUKUVLUYTVUFUVMVUAVUGXDVULUYLUXBUFZUYMUXKUFZVUKVUMJA
      UYLXEKBUYMXEUYCVUNVUOUJZUYPUWLUXNUVQVUPUYPUWLVFZVFZUXCUXLUYCVURVFUWPUXEVU
      PUXCUXLUJZUYCVUQVUPVUSUWRUYLVAZSEVBZUXGUYMVAZUAEVBZUJZUYCVUQVFVUNUXCVVAVU
      OUXLVVCVUNUXCVVAUXAVVATUYLUXBTUCXFUWTVUTSEUWSUYLUWRXGXHXIXJVUOUXLVVCUXJVV
      CUBUYMUXKUBUDXFUXIVVBUAEUXHUYMUXGXGXHXIXJXKVVDUVQVUQUXNVVAVVCUVQVUQVFZVUT
      VVCVVEVFSEVVCUWQEUFZVUTVVEVVBVVFVUTVVEVFZVFUAEVVFUXFEUFZVVBVVGUVQVVFVVHVV
      BVUTVUQUVOUVPVVFVVHVVBVUTVUQVFVFZVFZVFZUVNUVLUVQVVKVFUVMUVNVVFUVQVVJUVNVV
      FVVHUVQVVIUVNVVFVVHUVQVVIVFZUVNVVFVVHUHUWQUXFXLZEUFZVVLUWQUXFEXMVVNUVQVVB
      VUTVUQUVQVVBVUTUHZUYPDVVMUTZUWJVAZVVNUWLVVOVVPUYNVAZUYPVVQVFUVQVVBVUTVVRU
      VPVVBVUTVVRVFVFZUVOVVMUWQVAZUVPVVSVFZUWQUXFXNVVTUVRVVMUTZUWRVAZVWAVVMUWQU
      VRXOVWCVUTVVBUVPVVRVWCVUTVVBUVPVVRVFZVFZVVMUXFVAZVWCVUTUJVWBUYLVAZVWEUWQU
      XFXPVWBUWRUYLXQVWFUVTVVMUTZUXGVAZVWGVWEVFVVMUXFUVTXOVWIVVBVWGVWDVWIVVBVWG
      VWDVFVWIVVBUJVWHUYMVAZVWGVWDVWHUXGUYMXQVWGVWJVWDVWGVWJUJZUKVVPUTZUYLVAZUP
      VVPUTZUYMVAZUJZVWLVWNVJZUYNVAZVWDVWKVWPVWGVWMVWJVWOVWBVWLUYLUKDVVMXRXSVWH
      VWNUYMUPDVVMXRXSXTVNVWLUYLVWNUYMYAUVPVWRVVRUVPVVPYBZVVPVWQVAVWRVVRVFUVPVV
      PDYCZVAZVWTIVAZVWSDVVMYDFIDYEUXSVXAVXBUJVVPIVAZVWSPVVPVWTIXQUXSVXCVVPUXQV
      AZUXQYBVWSUXSVXCVXDIUXQVVPXGXCGHYFVVPUXQYGYHYIYJVVPYKVVPVWQUYNYLYMWNYMXJY
      NXJYOVQYIXJYPVQVMVOYQVVPUYNUWJYLVQVVNVVQUWLUWKVVQQVVMEUWHVVMVKUWIVVPUWJUW
      HVVMDYRYSYTXJUUAUUBVQUUCUUDYOUUEUUFUUGWMUUHWMUUHUUIUUJUUKWMUULUUFWMUUMUUN
      UUCUUOWKUUPUUQUURWLUUSUUQUUTUUIWNUVAUUKUVBUVCUVDXJUVEUVBUUIUWECUEUFZUVNFU
      WFDUIZUWDUWOVGUVOVXEUVPUWBUVLUVMVXEUVNABCLUVFWKUVGUVLUVMUVNUVPUWBUVHUVQVX
      FUWBUVOUVPVXFUVLUVMUVPVXFVFUVNUYGUVPVXFUYGIUWFVKUVPVXFVGUYGUWFUXQIUYIPUVI
      IUWFFDVLVQXCWKUUIWLUWCRDCEUWFFQUWFUVJMVRVSUVKXJ $.
      $( [15-Oct-2012] $)
  $}

  ${
    limptlimpr.1 $e |- T = ( R tX S ) $.
    limptlimpr.2 $e |- W = U. L $.
    limptlimpr.3 $e |- X = U. R $.
    limptlimpr.4 $e |- Y = U. S $.
    limptlimpr.5 $e |- Z = ( X X. Y ) $.
    $( A limit in a product topology exists iff the limits of the projections
       exist.  (Contributed by FL, 15-Oct-2012.) $)
    limptlimpr $p |- ( ( ( R e. Top /\ S e. Top /\ L e. Fil ) /\
        ( F : W --> Z /\ L2 e. A ) )
         -> ( <. L1 , L2 >. e. ( ( T fLimf L ) ` F )
         <-> ( L1 e. ( ( R fLimf L ) ` ( 1st o. F ) )
               /\ L2 e. ( ( S fLimf L ) ` ( 2nd o. F ) ) ) ) ) $=
      ( ctop wcel cfil w3a wf wa cop cflimf co cfv c1st ccom c2nd
      limptlimpr2lem1 wi limptlimpr2lem2 adantrr impbid ) BRSCRSFTSUAZGJEUBZLAS
      ZUCUCKLUDEDFUEUFUGSZKUHEUIBFUEUFUGSLUJEUICFUEUFUGSUCZABCDEFGHIJKLMNOPQUKU
      PUQUTUSULURBCDEFGHIJKLMNOPQUMUNUO $.
      $( [15-Oct-2012] $)
  $}

  $c fLimfrs $.

  $( Extend the definition of a class to include the limit of a function
     relatively to a subspace. $)
  cflimfrs $a class fLimfrs $.

  ${
    $d j k a p f u v $.
    $( Gives the limits of a function ` f : a --> U. j ` at a point
       ` p e. ( cls `` a ) ` relatively to a subspace ` a ` of a topology
       ` k ` .  ( The condition ` p e. ( cls `` a ) ` ensures the traces of the
       neighborhoods of ` p ` over ` a ` is a filter ( see ~ trnei ).  The set
       ` a ` can't be empty since its closure is not empty ( see ~ cldifemp ).
       Experimental. $)
    df-flimfrs $a |- fLimfrs = ( j e. Top , k e. Top |->
      ( a e. ~P U. k |->
         ( p e. ( ( cls ` k ) ` a ) , f e. ( U. j ^m a ) |->
            ( ( j fLimf { u | E. v e. ( ( nei ` k ) ` { p } )
                u = ( v i^i a ) } ) ` f ) ) ) ) $.
  $}

  ${
    $d v u A f p a $.  $d v u f P p $.  $d v u f j k p a $.
    $d v u f k K p a $.  $d f F $.  $d f j k J p a $.  $d f L $.
    islimrs.1 $e |- L = { u | E. v e. ( ( nei ` K ) ` { P } )
                u = ( v i^i A ) } $.
    islimrs.2 $e |- X = U. J $.
    islimrs.3 $e |- Y = U. K $.
    $( The limits of ` F ` at point ` P ` when one only considers the traces of
       the neighborhoods of ` P ` over ` A ` . ` F ` is a function from a part
       of ` U. K ` to ` U. J ` .  The point ` P ` must belong to
       ` ( ( cls `` K ) `` A ) ` (see also the comments under ~ df-flimfrs ) .
       (Contributed by FL, 15-Sep-2013.) $)
    islimrs $p |- ( ( ( J e. Top /\ K e. Top ) /\
      ( A C_ Y /\ P e. ( ( cls ` K ) ` A ) ) /\ F e. ( X ^m A ) ) ->
        ( ( ( J fLimfrs K ) ` A ) ` <. P , F >. )
          = ( ( J fLimf L ) ` F ) ) $=
      ( va vp vf vj vk ctop wcel wa wss ccl cfv cmap co w3a cop cflimfrs cuni
      cpw cv cin wceq csn cnei wrex cab cflimf cmpt2 cmpt cvv eqidd unieq
      opreq1d opreq1 fveq1d mpt2eq123dv adantr mpteq2dv pweq syl fveq2 ax-17
      eleq2d anbi1d rexbidv2 abbid opreq2d mpteq12dv df-flimfrs ovmpt2g simp1l
      simp1r uniexg pwexg adantl 3ad2ant1 mptexg syl3ancOLD opreq2 ineq2
      eqeq2d anbi2d eqid fvmptg wi sseq2 wb elpw2g biimprcd syl6bi ax-mp com12
      imp 3adant3 fvex oprex a1i mpt2exg sylancr sylancOLD df-opr sneq fveq2d
      abbidv eqcomi id fveq12d simp2r opreq1i eleq2i biimpi 3ad2ant3 syl5eqr
      3eqtrd ) FSTZGSTZUAZCJUBZDCGUCUDZUDZTZUAZEICUEUFZTZUGZDEUHZCFGUIUFZUDZUDY
      RCNGUJZUKZOPNULZYKUDZFUJZUUCUEUFZPULZFBULZAULZUUCUMZUNZAOULZUOZGUPUDZUDZU
      QZBURZUSUFZUDZUTZVAZUDZUDYROPYLUUECUEUFZUUGFUUHUUICUMZUNZAUUOUQZBURZUSUFZ
      UDZUTZUDZEFHUSUFZUDZYQYRYTUVBYQCYSUVAYGYHUVAVBTZYSUVAUNYQQRFGSSNRULZUJZUK
      ZOPUUCUVOUCUDZUDZQULZUJZUUCUEUFZUUGUVTUUKAUUMUVOUPUDZUDZUQZBURZUSUFZUDZUT
      ZVAUVAUINUVQOPUVSUUFUUGFUWFUSUFZUDZUTZVAVBUVTFUNZNUVQUWIUWLUWMUWIUWLUNUUC
      UVQTUWMOPUVSUWBUWHUVSUUFUWKUWMUVSVCUWMUWAUUEUUCUEUVTFVDVEUWMUUGUWGUWJUVTF
      UWFUSVFVGVHVIVJUVOGUNZNUVQUWLUUBUUTUWNUVPUUAUNUVQUUBUNUVOGVDUVPUUAVKVLUWN
      OPUVSUUFUWKUUDUUFUUSUWNUUCUVRYKUVOGUCVMVGUWNUUFVCUWNUUGUWJUURUWNUWFUUQFUS
      UWNUWEUUPBUWNBVNUWNUUKUUKAUWDUUOUWNUUIUWDTUUIUUOTZUUKUWNUWDUUOUUIUWNUUMUW
      CUUNUVOGUPVMVGVOVPVQVRVSVGVHVTABPQRONWAWBYGYHYNYPWCYGYHYNYPWDYQUUBVBTZUVN
      YIYNUWPYPYHUWPYGYHUUAVBTZUWPGSWEZUUAVBWFVLWGWHNUUBUUTVBWIVLWJVGVGYQYRUVBU
      VJCUUBTZUVJVBTZUVBUVJUNYQNCUUTUVJUUBVBUVAUUCCUNZOPUUDUUFUUSYLUVCUVIUUCCYK
      VMUUCCUUEUEWKUXAUUGUURUVHUXAUUQUVGFUSUXAUUPUVFBUXABVNUXAUUKUVEAUUOUUOUXAU
      UKUVEUWOUXAUUJUVDUUHUUCCUUIWLWMWNVQVRVSVGVHUVAWOWPYIYNUWSYPYIYNUWSYHYNUWS
      WQYGYNYHUWSYJYHUWSWQZYMJUUAUNZYJUXBWQMUXCYJCUUAUBZUXBJUUACWRYHUWSUXDYHUWQ
      UWSUXDWSUWRCUUAVBWTVLXAXBXCVIXDWGXEXFYQYLVBTUVCVBTZUWTCYKXGUXEYQUUECUEXHX
      IOPYLUVCUVIVBVBXJXKXLVGYQUVKDEUVJUFZUVMDEUVJXMYMEUVCTZUVMVBTZUXFUVMUNYQOP
      DEYLUVCUVIUVMUVJUUGFUVEADUOZUUNUDZUQZBURZUSUFZUDVBUULDUNZUUGUVHUXMUXNUVGU
      XLFUSUXNUVFUXKBUXNUVEUVEAUUOUXJUXNUWOUUIUXJTUVEUXNUUOUXJUUIUXNUUMUXIUUNUU
      LDXNXOVOVPVQXPVSVGUUGEUNZUUGEUXMUVLUXOUXLHFUSUXLHUNUXOHUXLKXQXIVSUXOXRXSU
      VJWOWBYIYJYMYPXTYPYIUXGYNYPUXGYOUVCEIUUECUELYAYBYCYDUXHYQEUVLXGXIWJYEYF
      $.
      $( [15-Sep-2013] $)
  $}

  $c IsolatedPt $.

  $( Extend class notation to include isolated points. $)
  cisopt $a class IsolatedPt $.

  ${
    $d j a x v $.
    $( Definition of an isolated point.  Experimental. $)
    df-islpt $a |- IsolatedPt = ( j e. Top |-> ( a e. ~P U. j |->
      { x e. a |
        E. v e. ( ( nei ` j ) ` { x } ) ( v i^i a ) = { x } } ) ) $.
  $}

