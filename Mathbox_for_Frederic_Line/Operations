$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z $.
    $( Two ways to state the domains of the variables of an operation. $)
    twsvbdop $p |-
      { <. <. x , y >. , z >. | ( <. x , y >. e. ( A X. B ) /\ ph ) } =
        { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ ph ) } $=
      ( cv cop cxp wcel wa visset opelxp anbi1i oprabbii ) BGZCGZHEFIJZAKPEJQFJ
      KZAKBCDRSAPQEFCLMNO $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${
    $d ch u $.  $d ph u x z $.  $d ph u y z $.  $d ps u $.
    ssoprab2g.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Inference of operation class abstraction subclass from implication. $)
    ssoprab2g $p |- ( ph -> { <. <. x , y >. , z >. | ps } C_
        { <. <. x , y >. , z >. | ch } ) $=
      ( vu cv cop wceq wa wex copab copab2 wi wal wss anim2d 19.22dvv
      19.21aivv ssopab2 sylibr dfoprab2 3sstr4g ) AHIDIEIJKZBLZEMDMZHFNZUFCLZEM
      DMZHFNZBDEFOCDEFOAUHUKPZFQHQUIULRAUMHFAUGUJDEABCUFGSTUAUHUKHFUBUCBDEFHUDC
      DEFHUDUE $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( The domain of an operation class abstraction.  Compare ~ dmoprabss . $)
    dmoprabsss $p |- dom { <. <. x , y >. , z >. |
    ( <. x , y >. e. ( A X. B ) /\ ph ) } C_ ( A X. B ) $=
      ( cv cop cxp wcel wa copab2 cdm twsvbdop dmeqi dmoprabss eqsstri ) BGZCGZ
      HEFIZJAKBCDLZMREJSFJKAKBCDLZMTUAUBABCDEFNOABCDEFPQ $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.  $d x y z S $.
    $d x ps $.  $d x y ch $.  $d x y z th $.
    oprabvaligg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    oprabvaligg.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    oprabvaligg.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    oprabvaligg.4 $e |- E* z ph $.
    oprabvaligg.5 $e |- F =
                  { <. <. x , y >. , z >. | ph } $.
    $( The value of an operation class abstraction.  See ~ oprabvalig .  The
       conditions ` ( x e. R /\ y e. S ) ` has been removed. $)
    oprabvaligg $p |- ( ( A e. R /\ B e. S /\ C e. D ) ->
                         ( th -> ( A F B ) = C ) ) $=
      ( wcel w3a cop copab2 cfv wceq co eloprabg wi wfun funoprab funopfvg
      mpan2 3ad2ant3 sylbird df-opr fveq1i eqtri eqeq1i syl6ibr ) HLTZIMTZJKTZU
      AZDHIUBZAEFGUCZUDZJUEZHINUFZJUEVCDVDJUBVETZVGABCDEFGHIJLMKOPQUGVBUTVIVGUH
      ZVAVBVEUIVJAEFGRUJVDJKVEUKULUMUNVHVFJVHVDNUDVFHINUOVDNVESUPUQURUS $.
      $( [14-Sep-1999] $)
  $}

  $( The value of a member of the domain of a subclass of an operation.  A more
     general version of ~ oprssoprv adapted to partial functions. $)
  oprssoprvg $p |- ( ( Fun F /\ G C_ F /\ <. A , B >. e. dom G )
    -> ( A F B ) = ( A G B ) ) $=
    ( wfun wss cop cdm wcel w3a cres co wceq cfv fvres df-opr 3eqtr4g 3ad2ant3
    wa funssres opreqd 3adant3 eqtr3d ) CEZDCFZABGZDHZIZJABCUGKZLZABCLZABDLZUHU
    DUJUKMUEUHUFUINUFCNUJUKUFUGCOABUIPABCPQRUDUEUJULMUHUDUESUIDABCDTUAUBUC $.
    $( [6-Oct-2009] $) $( [5-Oct-2009] $)

  ${
    $d A x y z $.  $d B z $.
    dmoprabss5.1 $e |- B e. C $.
    $( A version of ~ dmoprabss more adapted to partial operations. $)
    dmoprabss6 $p |- ( Rel A ->
     dom { <. <. x , y >. , z >. | ( <. x , y >. e. A /\ z = B ) } = A ) $=
      ( wrel cv cop wcel wceq wa wex copab copab2 cdm opabid2 19.42v opabbii
      pm3.26 elisseti isseti jctr impbii eqtri syl5eq dmoprab ) DHZAIBIJDKZCIEL
      ZMZCNZABOZDULABCPQUIUJABOZDUNABDRUNUJUKCNZMZABOUOUMUQABUJUKCSTUQUJABUQUJU
      JUPUAUJUPCEEFGUBUCUDUETUFUGULABCUHUG $.
      $( [18-Apr-2010] $) $( [18-Apr-2010] $)
  $}

  ${
    $d A z $.  $d B x y z $.  $d R x y z $.
    $( An adaption of ~ oprabex2g to partial operations.  Here must be a
       relation, but it can be something else than a cross product (on the
       contrary it is mandatory in ~ oprabex2g ). $)
    oprabex2gpop $p |- ( ( R e. B /\ Rel R ) -> { <. <. x , y >. , z >.
      | ( <. x , y >. e. R /\ z = A ) } e. _V ) $=
      ( cv cop wcel wceq wa copab2 cdm crn cxp wss cvv wrel ssexg relssdmrn
      adantl sseld anim1d ssoprab2g dmexg rnexg jca adantr eqid oprabex2g syl
      twsvbdop syl5eqel sylanc ) AGZBGZHZFIZCGDJZKZABCLZUQFMZFNZOZIZUSKZABCLZPV
      GQIVAQIFEIZFRZKZVAVGQSVJUTVFABCVJURVEUSVJFVDUQVIFVDPVHFTUAUBUCUDVJUOVBIUP
      VCIKUSKABCLZQVGVJVBQIZVCQIZKZVKQIVHVNVIVHVLVMFEUEFEUFUGUHABCVBVCDQQVKVKUI
      UJUKUSABCVBVCULUMUN $.
      $( [18-Apr-2010] $) $( [18-Apr-2010] $)
  $}

  ${
    $d F w x y z $.  $d R w x y z $.
    $( An adaptation of ~ fnoprv to partial operations. $)
    fnoprvpop $p |- ( Rel R -> ( F Fn R <->
    F = { <. <. x , y >. , z >. | ( <. x , y >. e. R /\ z = ( x F y ) ) } ) )
        $=
      ( vw wrel cv wcel cfv wceq wa copab cop co copab2 wfn wex wi eleq1
      biimpcd ancld 19.22dvv elrel syl5com ex pm2.43d biimpd eqcoms imp
      19.23aivv impbid1 anbi1d wb 19.41vv a1i anass fveq2 df-opr syl6eqr
      eqeq2d anbi2d pm5.32i bitri 2exbidv 3bitr2d opabbidv dfoprab2 dffn5
      syl5bb ) DGZEFHZDIZCHZVLEJZKZLZFCMZKEAHZBHZNZDIZVNVSVTEOZKZLZABCPZKEDQVKV
      RWFEVKVRVLWAKZWELZBRARZFCMWFVKVQWIFCVKVQWGWBLZBRARZVPLZWJVPLZBRARZWIVKVMW
      KVPVKVMWKVKVMWKVKVMVMWKSVMWGBRARWKVKVMLVMWGWJABVMWGWBWGVMWBVLWADTUAUBUCAB
      VLDUDUEUFUGWJVMABWGWBVMWBVMSWAVLWAVLKWBVMWAVLDTUHUIUJUKULUMWNWLUNVKWJVPAB
      UOUPVKWMWHABWMWHUNVKWMWGWBVPLZLWHWGWBVPUQWGWOWEWGVPWDWBWGVOWCVNWGVOWAEJWC
      VLWAEURVSVTEUSUTVAVBVCVDUPVEVFVGWEABCFVHUTVAFCDEVIVJ $.
      $( [18-Apr-2010] $) $( [18-Apr-2010] $)
  $}

  ${
    $d R w x y z $.  $d ph w $.  $d ps x y $.
    dfoprab4spop.1 $e |- ( <. x , y >. = w -> ( ph <-> ps ) ) $.
    $( An adaptation of ~ dfoprab4s to partial operations. $)
    dfoprab4spop $p |- ( Rel R ->
         { <. <. x , y >. , z >. | ( <. x , y >. e. R /\ ph ) }
       = { <. w , z >. | ( w e. R /\ ps ) } ) $=
      ( wrel cv cvv cxp wcel wa copab cop copab2 cin wss wb df-rel wceq df-ss
      incom eqtr eleq2d mpan sylbi elin syl5bbr anbi1d anass opabbidv eleq1
      anbi12d dfoprab3s syl5eq ) GIZFJZKKLZMZUSGMZBNZNZFEOVCFEOCJDJPZGMZANZCDEQ
      URVDVCFEURVAVBNZBNVCVDURVHVBBURUSUTGRZMZVBVHURGUTSZVJVBTZGUAVKGUTRZGUBZVL
      GUTUCVIVMUBZVNVLUTGUDVOVNNVIGUSVIVMGUEUFUGUHUHUSUTGUIUJUKVAVBBULUJUMVGVCC
      DEFVEUSUBVFVBABVEUSGUNHUOUPUQ $.
      $( [18-Apr-2010] $) $( [18-Apr-2010] $)
  $}


