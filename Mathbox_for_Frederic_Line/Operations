$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z $.
    $( Two ways to state the domain of an operation.  (Contributed by FL,
       24-Jan-2010.) $)
    twsvbdop $p |-
      { <. <. x , y >. , z >. | ( <. x , y >. e. ( A X. B ) /\ ph ) } =
        { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ ph ) } $=
      ( cv cop cxp wcel wa visset opelxp anbi1i oprabbii ) BGZCGZHEFIJZAKPEJQFJ
      KZAKBCDRSAPQEFCLMNO $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d ch u $.  $d ph u x z $.  $d ph u y z $.  $d ps u $.
    ssoprab2g.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Inference of operation class abstraction subclass from implication.
       (Contributed by FL, 24-Jan-2010.) $)
    ssoprab2g $p |- ( ph -> { <. <. x , y >. , z >. | ps } C_
        { <. <. x , y >. , z >. | ch } ) $=
      ( vu cv cop wceq wa wex copab copab2 wi wal wss anim2d 2eximdv 19.21aivv
      ssopab2b sylibr dfoprab2 3sstr4g ) AHIDIEIJKZBLZEMDMZHFNZUFCLZEMDMZHFNZBD
      EFOCDEFOAUHUKPZFQHQUIULRAUMHFAUGUJDEABCUFGSTUAUHUKHFUBUCBDEFHUDCDEFHUDUE
      $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( The domain of an operation class abstraction.  Compare ~ dmoprabss .
       (Contributed by FL, 24-Jan-2010.) $)
    dmoprabsss $p |- dom { <. <. x , y >. , z >. |
    ( <. x , y >. e. ( A X. B ) /\ ph ) } C_ ( A X. B ) $=
      ( cv cop cxp wcel wa copab2 cdm twsvbdop dmeqi dmoprabss eqsstri ) BGZCGZ
      HEFIZJAKBCDLZMREJSFJKAKBCDLZMTUAUBABCDEFNOABCDEFPQ $.
      $( [24-Jan-2010] $)
  $}

  $( Value returned by the operation ` G ` in terms of the value returned by
     the "super"-operation ` F ` .  (A version of ~ oprssoprv adapted to
     partial operations.)  (Contributed by FL, 5-Oct-2009.) $)
  oprssoprvg $p |- ( ( Fun F /\ G C_ F /\ <. A , B >. e. dom G )
    -> ( A F B ) = ( A G B ) ) $=
    ( wfun wss cop cdm wcel w3a cres co wceq cfv fvres df-opr 3eqtr4g 3ad2ant3
    wa funssres opreqd 3adant3 eqtr3d ) CEZDCFZABGZDHZIZJABCUGKZLZABCLZABDLZUHU
    DUJUKMUEUHUFUINUFCNUJUKUFUGCOABUIPABCPQRUDUEUJULMUHUDUESUIDABCDTUAUBUC $.
    $( [5-Oct-2009] $)

  ${
    $d A x y z $.  $d B z $.
    dmoprabss5.1 $e |- B e. C $.
    $( The domain of an operation class abstraction.  (A version of ~ dmoprabss
       adapted to partial operations.)  (Contributed by FL, 18-Apr-2010.) $)
    dmoprabss6 $p |- ( Rel A ->
     dom { <. <. x , y >. , z >. | ( <. x , y >. e. A /\ z = B ) } = A ) $=
      ( wrel cv cop wcel wceq wa copab2 cdm wex copab dmoprab 19.42v opabbii
      simpl elisseti isseti jctr impbii eqtri opabid2 syl5eq ) DHZAIBIJDKZCIELZ
      MZABCNOULCPZABQZDULABCRUIUNUJABQZDUNUJUKCPZMZABQUOUMUQABUJUKCSTUQUJABUQUJ
      UJUPUAUJUPCEEFGUBUCUDUETUFABDUGUHUH $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d A z $.  $d B x y z $.  $d R x y z $.
    $( Existence of an operation class abstraction.  (A version of ~ oprabex2g
       adapted to partial operations.)  (Contributed by FL, 18-Apr-2010.) $)
    oprabex2gpop $p |- ( ( R e. B /\ Rel R ) -> { <. <. x , y >. , z >.
      | ( <. x , y >. e. R /\ z = A ) } e. _V ) $=
      ( wcel wrel wa cv cop wceq copab2 cdm crn cxp wss cvv relssdmrn adantl
      sseld anim1d ssoprab2g twsvbdop dmexg rnexg jca adantr eqid oprabex2g
      syl syl5eqel ssexg syl11anc ) FEGZFHZIZAJZBJZKZFGZCJDLZIZABCMZUTFNZFOZPZG
      ZVBIZABCMZQVJRGVDRGUQVCVIABCUQVAVHVBUQFVGUTUPFVGQUOFSTUAUBUCUQVJURVEGUSVF
      GIVBIABCMZRVBABCVEVFUDUQVERGZVFRGZIZVKRGUOVNUPUOVLVMFEUEFEUFUGUHABCVEVFDR
      RVKVKUIUJUKULVDVJRUMUN $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d F w x y z $.  $d R w x y z $.
    $( Representation of an operation class abstraction in terms of its
       values.  (A version of ~ fnoprv adapted to partial operations.)
       (Contributed by FL, 18-Apr-2010.) $)
    fnoprvpop $p |- ( Rel R -> ( F Fn R <->
    F = { <. <. x , y >. , z >. | ( <. x , y >. e. R /\ z = ( x F y ) ) } ) )
        $=
      ( vw wfn cv wcel cfv wceq wa copab wrel cop co copab2 dffn5 wex wi elrel
      eleq1 biimpcd ancld 2eximdv syl5com ex pm2.43d biimpd eqcoms imp
      19.23aivv impbid1 anbi1d wb 19.41vv a1i anass fveq2 df-opr syl6eqr
      eqeq2d anbi2d pm5.32i bitri 2exbidv 3bitr2d opabbidv dfoprab2 syl5bb ) ED
      GEFHZDIZCHZVKEJZKZLZFCMZKDNZEAHZBHZOZDIZVMVSVTEPZKZLZABCQZKFCDERVRVQWFEVR
      VQVKWAKZWELZBSASZFCMWFVRVPWIFCVRVPWGWBLZBSASZVOLZWJVOLZBSASZWIVRVLWKVOVRV
      LWKVRVLWKVRVLVLWKTVRVLLWGBSASVLWKABVKDUAVLWGWJABVLWGWBWGVLWBVKWADUBUCUDUE
      UFUGUHWJVLABWGWBVLWBVLTWAVKWAVKKWBVLWAVKDUBUIUJUKULUMUNWNWLUOVRWJVOABUPUQ
      VRWMWHABWMWHUOVRWMWGWBVOLZLWHWGWBVOURWGWOWEWGVOWDWBWGVNWCVMWGVNWAEJWCVKWA
      EUSVSVTEUTVAVBVCVDVEUQVFVGVHWEABCFVIVAVBVJ $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d R w x y z $.  $d ph w $.  $d ps x y $.
    dfoprab4spop.1 $e |- ( <. x , y >. = w -> ( ph <-> ps ) ) $.
    $( Class abstraction for operations in terms of class abstraction of
       ordered pairs.  (A version of ~ dfoprab4s adapted to partial
       operations.)  (Contributed by FL, 18-Apr-2010.) $)
    dfoprab4spop $p |- ( Rel R ->
         { <. <. x , y >. , z >. | ( <. x , y >. e. R /\ ph ) }
       = { <. w , z >. | ( w e. R /\ ps ) } ) $=
      ( wrel cv cop wcel wa copab2 cvv cxp copab wceq eleq1 anbi12d dfoprab3s
      anass cin elin wss wb df-rel df-ss incom eqtr eleq2d mpan sylbi syl5bbr
      anbi1d opabbidv syl5eq ) GIZCJDJKZGLZAMZCDENFJZOOPZLZVBGLZBMZMZFEQVFFEQVA
      VFCDEFUSVBRUTVEABUSVBGSHTUAURVGVFFEVGVDVEMZBMURVFVDVEBUBURVHVEBVHVBVCGUCZ
      LZURVEVBVCGUDURGVCUEZVJVEUFZGUGVKGVCUCZGRZVLGVCUHVIVMRZVNVLVCGUIVOVNMVIGV
      BVIVMGUJUKULUMUMUNUOUNUPUQ $.
      $( [18-Apr-2010] $)
  $}


