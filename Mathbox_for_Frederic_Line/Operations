$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z $.
    $( Two ways to state the domain of an operation.  (Contributed by FL,
       24-Jan-2010.) $)
    twsvbdop $p |-
      { <. <. x , y >. , z >. | ( <. x , y >. e. ( A X. B ) /\ ph ) } =
        { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ ph ) } $=
      ( cv cop cxp wcel wa visset opelxp anbi1i oprabbii ) BGZCGZHEFIJZAKPEJQFJ
      KZAKBCDRSAPQEFCLMNO $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d ch u $.  $d ph u x z $.  $d ph u y z $.  $d ps u $.
    ssoprab2g.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Inference of operation class abstraction subclass from implication.
       (Contributed by FL, 24-Jan-2010.) $)
    ssoprab2g $p |- ( ph -> { <. <. x , y >. , z >. | ps } C_
        { <. <. x , y >. , z >. | ch } ) $=
      ( vu cv cop wceq wa wex copab copab2 wi wal wss anim2d dfoprab2 alrimivv
      2eximdv ssopab2b sylibr 3sstr4g ) AHIDIEIJKZBLZEMDMZHFNZUFCLZEMDMZHFNZBDE
      FOCDEFOAUHUKPZFQHQUIULRAUMHFAUGUJDEABCUFGSUBUAUHUKHFUCUDBDEFHTCDEFHTUE $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( The domain of an operation class abstraction.  Compare ~ dmoprabss .
       (Contributed by FL, 24-Jan-2010.) $)
    dmoprabsss $p |- dom { <. <. x , y >. , z >. |
    ( <. x , y >. e. ( A X. B ) /\ ph ) } C_ ( A X. B ) $=
      ( cv cop cxp wcel wa copab2 cdm twsvbdop dmeqi dmoprabss eqsstri ) BGZCGZ
      HEFIZJAKBCDLZMREJSFJKAKBCDLZMTUAUBABCDEFNOABCDEFPQ $.
      $( [24-Jan-2010] $)
  $}

  $( Value returned by the operation ` G ` in terms of the value returned by
     the "super"-operation ` F ` .  (A version of ~ oprssoprv adapted to
     partial operations.)  (Contributed by FL, 5-Oct-2009.) $)
  oprssoprvg $p |- ( ( Fun F /\ G C_ F /\ <. A , B >. e. dom G )
    -> ( A F B ) = ( A G B ) ) $=
    ( wfun wss cop cdm wcel w3a cres co wceq cfv fvres df-opr 3ad2ant3 funssres
    3eqtr4g wa opreqd 3adant3 eqtr3d ) CEZDCFZABGZDHZIZJABCUGKZLZABCLZABDLZUHUD
    UJUKMUEUHUFUINUFCNUJUKUFUGCOABUIPABCPSQUDUEUJULMUHUDUETUIDABCDRUAUBUC $.
    $( [5-Oct-2009] $)

  ${
    $d A x y z $.  $d B z $.
    dmoprabss5.1 $e |- B e. C $.
    $( The domain of an operation class abstraction.  (A version of ~ dmoprabss
       adapted to partial operations.)  (Contributed by FL, 18-Apr-2010.) $)
    dmoprabss6 $p |- ( Rel A ->
     dom { <. <. x , y >. , z >. | ( <. x , y >. e. A /\ z = B ) } = A ) $=
      ( wrel cv cop wcel wceq wa copab2 cdm wex copab dmoprab opabbii syl5eq
      19.42v simpl elisseti isseti jctr impbii eqtri opabid2 ) DHZAIBIJDKZCIELZ
      MZABCNOULCPZABQZDULABCRUIUNUJABQZDUNUJUKCPZMZABQUOUMUQABUJUKCUASUQUJABUQU
      JUJUPUBUJUPCEEFGUCUDUEUFSUGABDUHTT $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d A z $.  $d B x y z $.  $d R x y z $.
    $( Existence of an operation class abstraction.  (A version of ~ oprabex2g
       adapted to partial operations.)  (Contributed by FL, 18-Apr-2010.) $)
    oprabex2gpop $p |- ( ( R e. B /\ Rel R ) -> { <. <. x , y >. , z >.
      | ( <. x , y >. e. R /\ z = A ) } e. _V ) $=
      ( wcel wrel wa cv cop wceq copab2 cdm crn cxp wss cvv relssdmrn adantl
      sseld anim1d ssoprab2g twsvbdop dmexg rnexg jca adantr eqid oprabex2g syl
      syl5eqel ssexg syl11anc ) FEGZFHZIZAJZBJZKZFGZCJDLZIZABCMZUTFNZFOZPZGZVBI
      ZABCMZQVJRGVDRGUQVCVIABCUQVAVHVBUQFVGUTUPFVGQUOFSTUAUBUCUQVJURVEGUSVFGIVB
      IABCMZRVBABCVEVFUDUQVERGZVFRGZIZVKRGUOVNUPUOVLVMFEUEFEUFUGUHABCVEVFDRRVKV
      KUIUJUKULVDVJRUMUN $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d F w x y z $.  $d R w x y z $.
    $( Representation of an operation class abstraction in terms of its
       values.  (A version of ~ fnoprv adapted to partial operations.)
       (Contributed by FL, 18-Apr-2010.) $)
    fnoprvpop $p |- ( Rel R -> ( F Fn R <->
    F = { <. <. x , y >. , z >. | ( <. x , y >. e. R /\ z = ( x F y ) ) } ) )
        $=
      ( vw wfn cv wcel cfv wceq wa copab wex wi eleq1 wb a1i syl6eqr eqeq2d cop
      wrel co copab2 dffn5 elrel biimpcd ancld 2eximdv syl5com ex biimpd eqcoms
      pm2.43d exlimivv impbid1 anbi1d 19.41vv anass fveq2 df-opr anbi2d pm5.32i
      imp bitri 2exbidv 3bitr2d opabbidv dfoprab2 syl5bb ) EDGEFHZDIZCHZVKEJZKZ
      LZFCMZKDUBZEAHZBHZUAZDIZVMVSVTEUCZKZLZABCUDZKFCDEUEVRVQWFEVRVQVKWAKZWELZB
      NANZFCMWFVRVPWIFCVRVPWGWBLZBNANZVOLZWJVOLZBNANZWIVRVLWKVOVRVLWKVRVLWKVRVL
      VLWKOVRVLLWGBNANVLWKABVKDUFVLWGWJABVLWGWBWGVLWBVKWADPUGUHUIUJUKUNWJVLABWG
      WBVLWBVLOWAVKWAVKKWBVLWAVKDPULUMVDUOUPUQWNWLQVRWJVOABURRVRWMWHABWMWHQVRWM
      WGWBVOLZLWHWGWBVOUSWGWOWEWGVOWDWBWGVNWCVMWGVNWAEJWCVKWAEUTVSVTEVASTVBVCVE
      RVFVGVHWEABCFVISTVJ $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d R w x y z $.  $d ph w $.  $d ps x y $.
    dfoprab4spop.1 $e |- ( <. x , y >. = w -> ( ph <-> ps ) ) $.
    $( Class abstraction for operations in terms of class abstraction of
       ordered pairs.  (A version of ~ dfoprab4s adapted to partial
       operations.)  (Contributed by FL, 18-Apr-2010.) $)
    dfoprab4spop $p |- ( Rel R ->
         { <. <. x , y >. , z >. | ( <. x , y >. e. R /\ ph ) }
       = { <. w , z >. | ( w e. R /\ ps ) } ) $=
      ( wrel cv cop wcel wa copab2 cvv copab wceq cin sylbi syl5bbr cxp anbi12d
      eleq1 dfoprab3s anass elin wss df-rel df-ss incom eqtr eleq2d mpan anbi1d
      wb opabbidv syl5eq ) GIZCJDJKZGLZAMZCDENFJZOOUAZLZVBGLZBMZMZFEPVFFEPVAVFC
      DEFUSVBQUTVEABUSVBGUCHUBUDURVGVFFEVGVDVEMZBMURVFVDVEBUEURVHVEBVHVBVCGRZLZ
      URVEVBVCGUFURGVCUGZVJVEUOZGUHVKGVCRZGQZVLGVCUIVIVMQZVNVLVCGUJVOVNMVIGVBVI
      VMGUKULUMSSTUNTUPUQ $.
      $( [18-Apr-2010] $)
  $}


