$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Fields and Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d H g h $.  $d R a b g h $.  $d X a b g h $.
    com2i.1 $e |- G = ( 1st ` R ) $.
    com2i.2 $e |- H = ( 2nd ` R ) $.
    com2i.3 $e |- X = ran G $.
    $( Property of a commutative structure with two operation. $)
    com2i $p |- ( R e. Com2 -> A. a e. X A. b e. X ( a H b ) = ( b H a ) ) $=
      ( vh vg ccm2 wcel cv co wceq crn wral copab df-com2 eleq2i c1st cfv wb
      eqcomi eqeq2i rneq syl6eqr raleq raleqbi1dv syl sylbi c2nd opreq eqeq12d
      2ralbidv elopabi ) ALMAENZFNZJNZOZUSURUTOZPZFKNZQZRZEVERZKJSZMURUSCOZUSUR
      COZPZFDREDRZLVHAKJEFTUAVGVCFDRZEDRZVLKJAVDAUBUCZPVDBPZVGVNUDZVOBVDBVOGUEU
      FVPVEDPVQVPVEBQDVDBUGIUHVFVMEVEDVCFVEDUIUJUKULUTAUMUCZPZVCVKEFDDVSUTCPZVC
      VKUDVRCUTCVRHUEUFVTVAVIVBVJURUSUTCUNUSURUTCUNUOULUPUQUL $.
      $( [18-Feb-2010] $) $( [14-Feb-2010] $)
  $}

  ${
    $d X u $.
    $( The domain of the first variable of an internal operation with a left
       and right identity element equals its base set. $)
    rngmgmbs3 $p |- ( ( G : ( X X. X ) --> X /\
      E. u e. X A. x e. X ( ( x G u ) = x /\ ( u G x ) = x ) )
         -> dom dom G = X ) $=
      ( cv co wceq wa wral wrex cxp wf cdm c0 wne wi fdm dmeq eqtr ex dmxp
      syl5 syl rexn0 syl5com impcom ) AEZBEZCFUGGUHUGCFUGGHADIZBDJZDDKZDCLZCMZM
      ZDGZULDNOZUOUJULUMUKGZUPUOPZUKDCQUQUNUKMZGZURUMUKRUTUSDGZUOUPUTVAUOUNUSDS
      TDDUAUBUCUCUIBDUDUEUF $.
      $( [24-Jan-2010] $)
  $}

  ${
    mlrngmgex.1 $e |- H = ( 2nd ` R ) $.
    $( In a unital ring the range of the multiplication equals the domain of
       the first variable. $)
    rngdmdmrn $p |- ( R e. Ring -> dom dom H = ran H ) $=
      ( cring wcel c1st cfv crn cdm eqid rnplrnml2 rnplrnml eqtr3d ) ADEAFGZHBI
      IBHANBCNJZKANBCOLM $.
      $( [24-Jan-2010] $)
  $}

  ${
    rnplrnml3.1 $e |- G = ( 1st ` R ) $.
    $( In a unital ring the domain of the first operand of the addition equals
       the domain of the second operand of the addition. $)
    rnplrnml3 $p |- ( R e. Ring -> dom dom G = ran dom G ) $=
      ( cring wcel cgr cdm crn wceq ringgrp cxp wfo wf wi eqid grpfo fof fdm
      c0 wne dmeq dmxpid syl6eq eqtr ex rnxp syl5com rneq syl5 expcom eqcoms
      syl6 com13 mpcom grpn0 syl 3syl sylc ) ADEBFEZUSBGZGZUTHZIZABCJZVDUSBHZVE
      KZVEBLVFVEBMZUSVCNZBVEVEOZPVFVEBQVGUTVFIZVHVFVEBRVJVESTZVCUSVAVEIZVJVKVCN
      VJVAVFGVEUTVFUAVEUBUCVKVJVLVCVKVJVBVEIZVLVCNZVKVBVFHZIZVMVJVPVOVEIZVMVKVP
      VQVMVBVOVEUDUEVEVEUFUGUTVFUHUIVNVEVBVLVEVBIVCVAVEVBUDUJUKULUMUNBVEVIUOUIU
      PUQUR $.
      $( [18-Feb-2010] $) $( [11-Feb-2010] $)
  $}

  ${
    ununr.1 $e |- H = ( 2nd ` R ) $.
    ununr.2 $e |- X = ran ( 1st ` R ) $.
    $d H a x y $.  $d H x y z $.  $d R x y z $.  $d X a x y $.  $d X x y z $.
    $( The unit of a unital ring is unique. $)
    ununr $p |- ( R e. Ring -> E! x e. X
        A. y e. X ( ( y H x ) = y /\ ( x H y ) = y ) ) $=
      ( va vz cring wcel cv co wceq wa wral wrex wi wreu c1st cfv w3a cabl cxp
      wf eqid ringi simprd opreq1 id eqeq12d opreq2 anbi12d rcla4v eqeq1
      biimpd adantl com12 adantr syl6 com13 com24 imp4b rgen2a jctir eqeq1d
      ralbidv reu4 sylibr ) CJKZBLZALZDMZVKNZVLVKDMZVKNZOZBEPZAEQZVRVKHLZDMZVKN
      ZVTVKDMZVKNZOZBEPZOVLVTNZRZHEPAEPZOVRAESVJVSWIVJVOILZDMVLVKWJDMZDMNVLVKWJ
      CTUAZMDMVOVLWJDMZWLMNVLVKWLMWJDMWMWKWLMNUBIEPBEPAEPZVSVJWLUCKEEUDEDUEOWNV
      SOABICWLDEWLUFFGUGUHUHWHAHEVLEKZVTEKZVRWFWGWOWFVRWPWGWOWFVLVTDMZVLNZVTVLD
      MZVLNZOZVRWPWGRRWEXABVLEVKVLNZWBWRWDWTXBWAWQVKVLVKVLVTDUIXBUJZUKXBWCWSVKV
      LVKVLVTDULXCUKUMUNWPVRXAWGWPVRWSVTNZWQVTNZOZXAWGRZVQXFBVTEVKVTNZVNXDVPXEX
      HVMWSVKVTVKVTVLDUIXHUJZUKXHVOWQVKVTVKVTVLDULXIUKUMUNXDXGXEXAXDWGWTXDWGRWR
      WTXDWGWSVLVTUOUPUQURUSUTVAUTVBVCVDVEVRWFAHEWGVQWEBEWGVNWBVPWDWGVMWAVKVLVT
      VKDULVFWGVOWCVKVLVTVKDUIVFUMVGVHVI $.
      $( [3-Aug-2009] $) $( [12-Jul-2009] $)
  $}

  ${
    rnginvcl.1 $e |- X = ran G $.
    rnginvcl.2 $e |- N = ( inv ` G ) $.
    $( The additive inverse of a unital ring element pertains to the unital
       ring. $)
    rnginvcl $p |- ( ( <. G , H >. e. Ring /\ A e. X ) -> ( N ` A ) e. X ) $=
      ( cop cring wcel cfv cabl cgr wi fora ablgrp grpinvcl ex 3syl imp ) BCHIJ
      ZAEJZADKEJZUABLJBMJZUBUCNBCOBPUDUBUCABDEFGQRST $.
      $( [18-Apr-2010] $)
  $}

  ${
    multinv.1 $e |- X = ran G $.
    multinv.2 $e |- G = ( 1st ` R ) $.
    multinv.3 $e |- H = ( 2nd ` R ) $.
    $( Multiplication by an additive inverse. $)
    multinv $p |- ( ( R e. Ring /\ A e. X /\ B e. X )
    -> ( ( ( inv ` G ) ` A ) H B ) = ( ( inv ` G ) ` ( A H B ) ) ) $=
      ( cring wcel w3a co cgn cfv wceq cgi simp1 simp2 cgr wi ringgrp eqid
      grpinvcl ex syl a1dd 3imp simp3 wa ringdir eqcomd syl13anc 3ad2ant1
      grprinv syl11anc opreq1d ringlz 3adant2 3eqtrd wb ringcl syld3an2
      grpinvid1 syl111anc mpbird ) CJKZAFKZBFKZLZABEMZDNOZOZAVLOZBEMZVJVMVOPZVK
      VODMZDQOZPZVJVQAVNDMZBEMZVRBEMZVRVJVGVHVNFKZVIVQWAPVGVHVIRVGVHVISZVGVHVIW
      CVGVHWCVIVGDTKZVHWCUACDHUBZWEVHWCADVLFGVLUCZUDUEUFUGUHZVGVHVIUIVGVHWCVILU
      JWAVQAVNBCDEFHIGUKULUMVJVTVRBEVJWEVHVTVRPVGVHWEVIWFUNZWDAVRDVLFGVRUCZWGUO
      UPUQVGVIWBVRPVHBCDEFVRWJGHIURUSUTVJWEVKFKVOFKZVPVSVAWIABCDEFHIGVBVGWCVIWK
      VHVNBCDEFHIGVBWHVCVKVOVRDVLFGWJWGVDVEVFUL $.
      $( [7-Sep-2009] $) $( [2-Sep-2009] $)

    $( Multiplication by an additive inverse. $)
    multinvb $p |- ( ( R e. Ring /\ A e. X /\ B e. X )
     -> ( A H ( ( inv ` G ) ` B ) ) = ( ( inv ` G ) ` ( A H B ) ) ) $=
      ( cring wcel w3a co cgn cfv wceq cgi cgr ringgrp 3ad2ant1 simp3 eqid
      grprinv syl11anc opreq2d simp1 simp2 wi grpinvcl ex syl a1d 3imp ringdi
      syl13anc ringrz 3adant3 3eqtr3d wb ringcl syld3an3 grpinvid1 syl111anc
      mpbird eqcomd ) CJKZAFKZBFKZLZABEMZDNOZOZABVKOZEMZVIVLVNPZVJVNDMZDQOZPZVI
      ABVMDMZEMZAVQEMZVPVQVIVSVQAEVIDRKZVHVSVQPVFVGWBVHCDHSZTZVFVGVHUAZBVQDVKFG
      VQUBZVKUBZUCUDUEVIVFVGVHVMFKZVTVPPVFVGVHUFVFVGVHUGWEVFVGVHWHVFVHWHUHZVGVF
      WBWIWCWBVHWHBDVKFGWGUIUJUKULUMZABVMCDEFHIGUNUOVFVGWAVQPVHACDEFVQWFGHIUPUQ
      URVIWBVJFKVNFKZVOVRUSWDABCDEFHIGUTVFVGWHWKVHAVMCDEFHIGUTWJVAVJVNVQDVKFGWF
      WGVBVCVDVE $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    mult2inv.1 $e |- X = ran G $.
    mult2inv.2 $e |- G = ( 1st ` R ) $.
    mult2inv.3 $e |- H = ( 2nd ` R ) $.
    $( Multiplication of two additive inverses. $)
    mult2inv $p |- ( ( R e. Ring /\ A e. X /\ B e. X )
     -> ( ( ( inv ` G ) ` A ) H ( ( inv ` G ) ` B ) ) = ( A H B ) ) $=
      ( cring wcel w3a cgn cfv co wceq multinv cgr ringgrp 3ad2ant1 simp3 eqid
      grpinvcl syl11anc syld3an3 multinvb fveq2d ringcl grp2inv 3eqtrd ) CJKZAF
      KZBFKZLZADMNZNBUONZEOZAUPEOZUONZABEOZUONZUONZUTUKULUPFKZUQUSPUMAUPCDEFGHI
      QUNDRKZUMVCUKULVDUMCDHSTZUKULUMUABDUOFGUOUBZUCUDUEUNURVAUOABCDEFGHIUFUGUN
      VDUTFKVBUTPVEABCDEFHIGUHUTDUOFGVFUIUDUJ $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    $d H u x $.  $d X u x $.
    rngunval2.1 $e |- X = ran ( 1st ` R ) $.
    rngunval2.2 $e |- H = ( 2nd ` R ) $.
    rngunval2.3 $e |- U = ( Id ` H ) $.
    $( The value of the unit of a ring. $)
    rngunval2 $p |- ( R e. Ring -> U = U. { u e. X |
     A. x e. X ( ( u H x ) = x /\ ( x H u ) = x ) } ) $=
      ( cring wcel cv co wceq wa crn wral crab cuni cvv c2nd cfv a1i fvex
      syl6eqel eqid idrval syl c1st rnplrnml syl5eq rabeq raleq rabbidv eqtrd
      unieqd eqtr4d ) CJKZDBLZALZEMUTNUTUSEMUTNOZAEPZQZBVBRZSZVAAFQZBFRZSURETKD
      VENURECUAUBZTEVHNURHUCCUAUDUEABTDEVBVBUFIUGUHURVGVDURFVBNZVGVDNURCUIUBZPV
      BFCVJEHVJUFUJGUKVIVGVFBVBRVDVFBFVBULVIVFVCBVBVAAFVBUMUNUOUHUPUQ $.
      $( [18-Feb-2010] $) $( [12-Feb-2010] $)
  $}

  ${
    $d G x y $.  $d H x y $.
    $( The predicate "is a field". $)
    isfld $p |- ( ( G e. A /\ H e. B ) -> ( <. G , H >. e. Fld <->
      ( <. G , H >. e. DivRing
      /\ A. x e. ran G A. y e. ran G ( x H y ) = ( y H x ) ) ) ) $=
      ( wcel wa cop cfld cdrng ccm2 cin cv co wceq crn wral df-fld a1i eleq2d
      wb elin iscom2 anbi2d 3bitrd ) ECGFDGHZEFIZJGUHKLMZGZUHKGZUHLGZHZUKANZBNZ
      FOUOUNFOPBEQZRAUPRZHUGJUIUHJUIPUGSTUAUJUMUBUGUHKLUCTUGULUQUKCDEFABUDUEUF
      $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    fldi.1 $e |- G = ( 1st ` R ) $.
    fldi.2 $e |- H = ( 2nd ` R ) $.
    fldi.3 $e |- X = ran G $.
    fldi.4 $e |- Z = ( Id ` G ) $.
    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( The "axioms" of a field. $)
      fldi $p |- ( R e. Fld ->
         (
              ( G e. Abel /\ H : ( X X. X ) --> X )
              /\ (
                 A. x e. X A. y e. X A. z e. X (
                         ( ( x H y ) H z ) = ( x H ( y H z ) )
                        /\ ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) )
                        /\ ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) )
                 /\ E. x e. X A. y e. X (
                         ( y H x ) = y
                        /\ ( x H y ) = y ) )
               /\ (
                  ( H |` ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. Grp
                 /\ A. x e. X A. y e. X ( x H y ) = ( y H x ) ) ) ) $=
        ( cfld wcel cdrng ccm2 cin cabl cxp wf wa cv co wceq w3a wral wrex csn
        cdif cres cgr df-fld eleq2i elin cring wi drngi ringi id 3expia syl
        expdimp com2i syl5 imp sylbi ) DMNDOPQZNZERNGGSGFTUAZAUBZBUBZFUCZCUBZFU
        CVJVKVMFUCZFUCUDVJVKVMEUCFUCVLVJVMFUCZEUCUDVJVKEUCVMFUCVOVNEUCUDUECGUFB
        GUFAGUFVKVJFUCZVKUDVLVKUDUABGUFAGUGUAZFGHUHUIZVRSUJUKNZVLVPUDBGUFAGUFZU
        AZUEZMVGDULUMVHDONZDPNZUAWBDOPUNWCWDWBWCDUONZVSUAZWDWBUPDEFGHIJKLUQWFVT
        WBWDWEVSVTWBWEVIVQUAWAWBUPABCDEFGIJKURVIVQWAWBWBUSUTVAVBDEFGABIJKVCVDVA
        VEVFVF $.
        $( [18-Sep-2010] $) $( [15-Sep-2010] $)
    $}
  $}

  ${
    $d G x y z $.  $d R x y z $.
    fldax1.1 $e |- G = ( 1st ` R ) $.
    $( 1st "axiom" of a field.  The addition is an abelian group. $)
    fldax1 $p |- ( R e. Fld -> G e. Abel ) $=
      ( vx vy vz cfld wcel cabl crn cxp c2nd cfv wf wa cv co wceq w3a wral
      wrex cgi csn cdif cres cgr eqid fldi simp1d simpld ) AGHZBIHZBJZUMKUMALMZ
      NZUKULUOODPZEPZUNQZFPZUNQUPUQUSUNQZUNQRUPUQUSBQUNQURUPUSUNQZBQRUPUQBQUSUN
      QVAUTBQRSFUMTEUMTDUMTUQUPUNQZUQRURUQROEUMTDUMUAOUNUMBUBMZUCUDZVDKUEUFHURV
      BREUMTDUMTODEFABUNUMVCCUNUGUMUGVCUGUHUIUJ $.
      $( [11-Jul-2010] $)
  $}

  ${
    fldax.1 $e |- G = ( 1st ` R ) $.
    fldax.2 $e |- H = ( 2nd ` R ) $.
    fldax.3 $e |- X = ran G $.

    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 2nd "axiom" of a field.  The multiplication is an internal
         operation. $)
      fldax2 $p |- ( R e. Fld -> H : ( X X. X ) --> X ) $=
        ( vx vy vz cfld wcel cabl cxp wf wa cv co wceq w3a wral wrex cgi cfv
        csn cdif cres cgr eqid fldi simp1d simprd ) AKLZBMLZDDNDCOZUMUNUOPHQZIQ
        ZCRZJQZCRUPUQUSCRZCRSUPUQUSBRCRURUPUSCRZBRSUPUQBRUSCRVAUTBRSTJDUAIDUAHD
        UAUQUPCRZUQSURUQSPIDUAHDUBPCDBUCUDZUEUFZVDNUGUHLURVBSIDUAHDUAPHIJABCDVC
        EFGVCUIUJUKUL $.
        $( [11-Jul-2010] $)
    $}

    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 3rd "axiom" of a field.  The multiplication is associative. $)
      fldax3 $p |- ( R e. Fld -> A. x e. X A. y e. X A. z e. X
                         ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $=
        ( cfld wcel cv co wceq w3a wral wa wrex cabl cxp wf cgi cfv csn cdif
        cres cgr eqid fldi simp2d simp1 ralimi adantr syl ) DKLZAMZBMZFNZCMZFNU
        QURUTFNZFNOZUQURUTENFNUSUQUTFNZENOZUQURENUTFNVCVAENOZPZCGQZBGQZAGQZURUQ
        FNZUROUSURORBGQAGSZRZVBCGQZBGQZAGQZUPETLGGUAGFUBRVLFGEUCUDZUEUFZVQUAUGU
        HLUSVJOBGQAGQRABCDEFGVPHIJVPUIUJUKVIVOVKVHVNAGVGVMBGVFVBCGVBVDVEULUMUMU
        MUNUO $.
        $( [11-Jul-2010] $)
    $}


    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 4th "axiom" of a field.  The multiplication is distributive. $)
      fldax4 $p |- ( R e. Fld -> A. x e. X A. y e. X A. z e. X
                        ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $=
        ( cfld wcel cv co wceq w3a wral wa wrex cabl cxp wf cgi cfv csn cdif
        cres cgr eqid fldi simp2d simp2 ralimi adantr syl ) DKLZAMZBMZFNZCMZFNU
        QURUTFNZFNOZUQURUTENFNUSUQUTFNZENOZUQURENUTFNVCVAENOZPZCGQZBGQZAGQZURUQ
        FNZUROUSURORBGQAGSZRZVDCGQZBGQZAGQZUPETLGGUAGFUBRVLFGEUCUDZUEUFZVQUAUGU
        HLUSVJOBGQAGQRABCDEFGVPHIJVPUIUJUKVIVOVKVHVNAGVGVMBGVFVDCGVBVDVEULUMUMU
        MUNUO $.
        $( [11-Jul-2010] $)
    $}


    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 5th "axiom" of a field.  Existence of a neutral element. $)
      fldax5 $p |- ( R e. Fld -> E. x e. X A. y e. X ( y H x ) = y ) $=
        ( vz cfld wcel cv co wceq wa wral wrex w3a cabl cxp wf cgi cfv csn
        cdif cres cgr eqid fldi simp2d simprd simpl ralimi reximi syl ) CKLZBMZ
        AMZENZUROZUSURENZUROZPZBFQZAFRZVABFQZAFRUQVBJMZENUSURVHENZENOUSURVHDNEN
        VBUSVHENZDNOUSURDNVHENVJVIDNOSJFQBFQAFQZVFUQDTLFFUAFEUBPVKVFPEFDUCUDZUE
        UFZVMUAUGUHLVBUTOBFQAFQPABJCDEFVLGHIVLUIUJUKULVEVGAFVDVABFVAVCUMUNUOUP
        $.
        $( [11-Jul-2010] $)
    $}

    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      fldax.4 $e |- Z = ( Id ` G ) $.
      $( 6th "axiom" of a field.  The multiplication is a group on the
         underlying set deprived from zero. $)
      fldax6 $p |- ( R e. Fld -> ( H |`
        ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. Grp ) $=
        ( vx vy vz cfld wcel csn cdif cxp cres cgr cv co wceq wral cabl wf wa
        w3a wrex fldi simp3d simpld ) AMNZCDEOPZUMQRSNZJTZKTZCUAZUPUOCUAZUBKDUC
        JDUCZULBUDNDDQDCUEUFUQLTZCUAUOUPUTCUAZCUAUBUOUPUTBUACUAUQUOUTCUAZBUAUBU
        OUPBUAUTCUAVBVABUAUBUGLDUCKDUCJDUCURUPUBUQUPUBUFKDUCJDUHUFUNUSUFJKLABCD
        EFGHIUIUJUK $.
        $( [11-Jul-2010] $)
    $}

    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 7th "axiom" of a field.  The multiplication is commutative. $)
      fldax7 $p |- ( R e. Fld
        -> A. x e. X A. y e. X ( x H y ) = ( y H x ) ) $=
        ( vz cfld wcel cgi cfv csn cdif cxp cres cgr cv co wceq wral cabl wf
        wa w3a wrex eqid fldi simp3d simprd ) CKLZEFDMNZOPZUOQRSLZATZBTZEUAZURU
        QEUAZUBBFUCAFUCZUMDUDLFFQFEUEUFUSJTZEUAUQURVBEUAZEUAUBUQURVBDUAEUAUSUQV
        BEUAZDUAUBUQURDUAVBEUAVDVCDUAUBUGJFUCBFUCAFUCUTURUBUSURUBUFBFUCAFUHUFUP
        VAUFABJCDEFUNGHIUNUIUJUKUL $.
        $( [11-Jul-2010] $)
    $}

  $}

  ${
    zrfld.1 $e |- A e. B $.
    $( The zero ring is not a field. $)
    zrfld $p |- -. <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >.
      e. Fld $=
      ( cop csn cfld wcel cdrng ccm2 cin wa zrdivrng intnanr elin mtbir df-fld
      eleq2i ) AADADEZRDZFGSHIJZGZUASHGZSIGZKUBUCABCLMSHINOFTSPQO $.
      $( [24-Jan-2010] $)
  $}

  ${
    zerdivemp.1 $e |- G = ( 1st ` R ) $.
    zerdivemp.2 $e |- H = ( 2nd ` R ) $.
    zerdivemp.3 $e |- Z = ( Id ` G ) $.
    zerdivemp.4 $e |- X = ran G $.
    zerdivemp.5 $e |- U = ( Id ` H ) $.
    ${
      $d A a $.  $d B a $.  $d H a $.  $d R a $.  $d X a $.  $d Z a $.
      $( In a unitary ring a left invertible element is not a zero divisor. $)
      zerdivemp1 $p |- ( ( R e. Ring /\ A e. ( X \ Z ) /\
      E. a e. X ( a H A ) = U ) ->
         ( B e. X -> ( ( A H B ) = Z -> B = Z ) ) ) $=
        ( cring wcel cdif cv co wceq wrex wi w3a wa simpl1 simpr1 eldifi
        3ad2ant3 adantl simpl3 3jca jca ringass eqtr ex opreq1 ringrz 3adant3
        crn c1st cfv rneqi eqtri ringlidm 3adant2 simp1 simp2 simp3 3eqtr3d
        a1d 3exp com14 com13 sylc com15 com24 syl eqcoms com25 com12 3imp syl6
        3imp1 3syl pm2.43i 3exp1 opreq2 syl5com r19.23aiv ) COPZAGHQPZIRZAFSZDT
        ZIGUAZBGPZABFSZHTZBHTZUBUBZWOWKWJWTWNWKWJWTUBZUBIGWLGPZWNWKXAWRWJWPXBWN
        WKUCZWSWJWLWQFSZWLHFSZTZWPXCWSUBUBWRWJXFWPXCWSWJXFWPUCZXCUDZWSXHWJXBAGP
        ZWPUCZUDWMBFSZXDTZXHWSUBXHWJXJWJXFWPXCUEXHXBXIWPXGXBWNWKUFXCXIXGWKXBXIW
        NAGHUGUHUIWJXFWPXCUJUKULWLABCEFGJKMUMXHXLWSWJXFWPXCXLWSUBXLXFWPXCWJWSXL
        XFXKXETZWPXCWJWSUBZUBUBXLXFXMXKXDXEUNUOXCWPXMXNXBWNWKWPXMXNUBUBZWNXBWKX
        OUBZWNXKDBFSZTZXBXPUBWMDBFUPXRXMWKWPXBXNXMWKWPXBXNUBUBUBZUBXQXKXQXKTZXM
        XSXTXMUDXQXETZXSXQXKXEUNYAXBWPWKXNWJXBWPWKYAWSWJXBWPWKYAWSUBZUBZWJXBWPU
        CXEHTZXQBTZYCWJXBYDWPWLCEFGHLMJKUQURWJWPYEXBBCDFGKGEUSCUTVAZUSMEYFJVBVC
        NVDVEWKYEYDYBYAYEYDWKWSYAYEYDWKWSUBYAYEYDUCZWSWKYGXQXEBHYAYEYDVFYAYEYDV
        GYAYEYDVHVIVJVKVLVMVNVKVOVPVQUOVRVSVQVTWAVMWBVOWCVTWDWEWFWQHWLFWGWHVLVK
        WIVMWA $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d A a $.  $d R a $.  $d U a $.  $d X a $.  $d Z a $.
      $( In a unitary ring a left invertible element is different from zero iff
         ` 1 =/= 0 ` . $)
      rngridfz $p |- ( ( R e. Ring /\ A e. X /\
         E. a e. X ( a H A ) = U ) -> ( A =/= Z <-> U =/= Z ) ) $=
        ( cring wcel cv co wceq wrex w3a wb wi wa eqeq12 biimpd ex ringrz
        syl5com com3l imp 3adant3 opreq2 syl5 crn c1st cfv rneqi eqtri
        ringridm syl11anc expcom 3ad2ant3 impbid 3exp1 r19.23aiv com13 3imp
        necon3bid ) BNOZAFOZHPZAEQZCRZHFSZTAGCGVIVJVNAGRZCGRZUAZVNVJVIVQVMVJVIV
        QUBUBHFVKFOZVMVJVIVQVRVMVJTZVIUCZVOVPVTVLVKGEQZRZVPVOVSVIWBVPUBZVRVMVIW
        CUBZVJVRVMWDVIVRVMWCVIVRVMWCUBVMWAGRZWCVIVRUCVMWEWCVMWEUCWBVPVLCWAGUDUE
        UFVKBDEFGKLIJUGUHUFUIUJUKUJAGVKEULUMVTACEQZAGEQZRZVOVPVSVIWHVOUBZVJVRVI
        WIUBVMVIVJWIVIVJUCWFARZWGGRZWIABCEFJFDUNBUOUPZUNLDWLIUQURMUSABDEFGKLIJU
        GWJWKUCWHVOWFAWGGUDUEUTVAVBUJCGAEULUMVCVDVEVFVGVH $.
        $( [18-Apr-2010] $)
    $}
  $}

  ${
    $d x y $.
    $( ` ZZ ` is a commutative ring. $)
    zintdom $p |- <. + , x. >. e. ( Ring i^i Com2 ) $=
      ( vx vy caddc cmul cop cring ccm2 cin wcel elin cnring cv co wceq crn
      wral cc axmulcom cxp wfo cabl cnaddabl cgr cmnd ablgrp grpmnd cmagm
      cexid mndmgmid wf cdm axaddopr fdm dmeq wi dmxpid eqtr expcom eqcoms
      com12 ax-mp 3syl opidon syl forn eleq2i syl2anb rgen2a cvv wb addex
      mulex iscom2 mp2an mpbir mpbir2an ) CDEZFGHIVQFIVQGIZVQFGJKVRALZBLZDMVTVS
      DMNZBCOZPAWBPZWAABWBVSQIVTQIWAVSWBIVTWBIVSVTRWBQVSQQSZQCTZWBQNCUAIZWEUBWF
      CUCICUDIZWECUECUFWGCUGUHHIWECUICQWDQCUJZQCUKZUKZNZULWHWIWDNWJWDUKZNZWKWDQ
      CUMWIWDUNWLQNWMWKUOZQUPWNQWLWMQWLNZWKWOWKUOWLWJWOWLWJNWKQWLWJUQURUSUTUSVA
      VBVAVCVDVBVAWDQCVEVAZVFWBQVTWPVFVGVHCVIIDVIIVRWCVJVKVLVIVICDABVMVNVOVP $.
      $( [18-Apr-2010] $)
  $}

  $c Tofld $.

  $( Extend class notation with the class of all totally ordered fields. $)
  ctofld $a class Tofld $.

  ${
    $d g h r x y z $.
    $( Definition of a totally ordered field.  Experimental. $)
    df-tofld $a |- Tofld = { <. <. g , h >. , r >. | ( <. g , h >. e. Fld
     /\ ( r e. Toset /\ U. U. r = ran g ) /\ A. x e. U. U. r A. y e. U. U. r
        A. z e. U. U. r ( ( x r y -> ( x g z ) r ( y g z ) ) /\
        ( ( Id ` g ) r z -> ( x h z ) r ( y h z ) ) ) ) } $.
  $}

  $c zeroDiv $.

  $( Extend class notation with the class of all the zero divisors. $)
  czerodiv $a class zeroDiv $.

  $( Definition of the zero divisors of a ring.  Experimental. $)
  df-zd $a |- zeroDiv = { <. r , y >. | y = { a e. ran ( 1st ` r ) |
      ( a =/= ( Id ` ( 1st ` r ) ) /\
        E. b e. ran ( 1st ` r )
           ( b =/= ( Id ` ( 1st ` r ) ) /\
           ( a ( 2nd ` r ) b ) = ( Id ` ( 1st ` r ) ) ) ) } } $.

