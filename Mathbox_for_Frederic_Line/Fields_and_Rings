$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Fields and Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c Com2 $.

  $( Extend class notation with a class that adds commutativity to various
    flavors of rings. $)
  ccm2 $a class Com2 $.


  ${ $d g h a b $.
   $( A device to add commutativity to various sorts of rings. I use
      ` ran g ` because I suppose ` g ` has a neutral element and
      therefore is onto. Experimental. Don't use for the moment. $)
   df-com2 $a |- Com2 = { <. g , h >. | A. a e. ran g A. b e. ran g
     ( a h b ) = ( b h a ) } $.
  $}

  ${ $d G a b x y $. $d H a b x y $.
   $( A device to add commutativity to various sorts of rings. $)
   iscom2 $p |- ( ( G e. A /\ H e. B ) -> ( <. G , H >. e. Com2 <->
     A. a e. ran G A. b e. ran G ( a H b ) = ( b H a ) ) ) $=
     ( vy vx wcel wa cop ccm2 cv co wceq crn wral copab df-com2 a1i eleq2d
     rneq raleq1d raleq12d eqidd opreq eqeq12d opelopabg bitrd ) CAIDBIJZCDKZLI
     UKEMZFMZGMZNZUMULUNNZOZFHMZPZQZEUSQZHGRZIULUMDNZUMULDNZOZFCPZQZEVFQZUJLVBU
     KLVBOUJHGEFSTUAVAUQFVFQZEVFQVHHGCDABURCOZUTVIEUSVFURCUBZVJUQFUSVFVKUCUDUND
     OZVIVGEVFVFVLVFUEZVLUQVEFVFVFVMVLUOVCUPVDULUMUNDUFUMULUNDUFUGUDUDUHUI $.
     $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    $d H g h $. $d R a b g h $. $d X a b g h $.
    com2i.1 $e |- G = ( 1st ` R ) $.
    com2i.2 $e |- H = ( 2nd ` R ) $.
    com2i.3 $e |- X = ran G $.
    $( Property of a commutative structure with two operation. $)
    com2i $p |- ( R e. Com2 -> A. a e. X A. b e. X ( a H b ) = ( b H a ) ) $=
      ( vh vg ccm2 wcel cv co wceq crn wral copab df-com2 eleq2i c1st cfv wb
      eqcomi eqeq2i rneq syl6eqr raleq1 raleqd syl sylbi c2nd opreq eqeq12d
      2ralbidv elopabi ) ALMAENZFNZJNZOZUSURUTOZPZFKNZQZRZEVERZKJSZMURUSCOZUSUR
      COZPZFDREDRZLVHAKJEFTUAVGVCFDRZEDRZVLKJAVDAUBUCZPVDBPZVGVNUDZVOBVDBVOGUEU
      FVPVEDPVQVPVEBQDVDBUGIUHVFVMEVEDVCFVEDUIUJUKULUTAUMUCZPZVCVKEFDDVSUTCPZVC
      VKUDVRCUTCVRHUEUFVTVAVIVBVJURUSUTCUNUSURUTCUNUOULUPUQUL $.
      $( [18-Feb-2010] $) $( [14-Feb-2010] $)
  $}


  ${ $d v w x y z $.
   $( The class of all unital rings is a relation. $)
   relrng $p |- Rel Ring $=
     ( vw vv vx vy vz cring wrel cv cabl wcel crn cxp wf wa co wceq w3a wral
     wrex copab relopab df-ring releqi mpbir ) FGAHZIJUEKZUFLUFBHZMNCHZDHZUGOZE
     HZUGOUHUIUKUGOZUGOPUHUIUKUEOUGOUJUHUKUGOZUEOPUHUIUEOUKUGOUMULUEOPQEUFRDUFR
     CUFRUIUHUGOUIPUJUIPNDUFRCUFSNNZABTZGUNABUAFUOCDEABUBUCUD $.
     $( [7-Sep-2009] $) $( [31-Aug-2009] $)
  $}


  ${
    rngn0.1 $e |- G = ( 1st ` R ) $.
    rngn0.2 $e |- X = ran G $.
    $( The base set of a ring is not empty. $)
    rngn0 $p |- ( R e. Ring -> X =/= (/) ) $=
      ( cring wcel cgr c0 wne ringgrp grpn0 syl ) AFGBHGCIJABDKBCELM $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${ $d X u $.
    $( The domain of the first variable of an internal operation with a
       left and right identity element equals its base set. $)
    rngmgmbs3 $p |- ( ( G : ( X X. X ) --> X /\
      E. u e. X  A. x e. X ( ( x G u ) = x /\ ( u G x ) = x ) )
         -> dom dom G = X ) $=
      ( cv co wceq wa wral wrex cxp wf cdm c0 wne wi fdm dmeq eqtr ex dmxp
      syl5 syl rexn0 syl5com impcom ) AEZBEZCFUGGUHUGCFUGGHADIZBDJZDDKZDCLZCMZM
      ZDGZULDNOZUOUJULUMUKGZUPUOPZUKDCQUQUNUKMZGZURUMUKRUTUSDGZUOUPUTVAUOUNUSDS
      TDDUAUBUCUCUIBDUDUEUF $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${ $d G u x z $. $d G u y z $. $d X u x z $. $d X u y z $.
    $( The range of an internal operation with a left and right identity
       element equals its base set. $)
    rngmgmbs4 $p |- ( ( G : ( X X. X ) --> X /\
      E. u e. X  A. x e. X ( ( x G u ) = x /\ ( u G x ) = x ) )
         -> ran G = X ) $=
      ( vz vy cxp wf cv co wceq wa wral wrex wfo crn wcel weq opreq2 id
      eqeq12d rcla4v eqeq1d eqcom syl5bb rcla4ev ex syld pm3.27 r19.20si syl5
      r19.22sdv impcom r19.21aiva anim2i fooprv sylibr forn syl ) DDGZDCHZAIZBI
      ZCJVBKZVCVBCJZVBKZLZADMZBDNZLZUTDCOZCPDKVJVAEIZVCFIZCJZKZFDNZBDNZEDMZLVKV
      IVRVAVIVQEDVLDQZVIVQVSVHVPBDVSVFADMZVPVHVSVTVCVLCJZVLKZVPVFWBAVLDAERZVEWA
      VBVLVBVLVCCSWCTUAUBVSWBVPVOWBFVLDFERZVNVLKWBVOWDVNWAVLVMVLVCCSUCVLVNUDUEU
      FUGUHVGVFADVDVFUIUJUKULUMUNUOBFEDDDCUPUQUTDCURUS $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}



  ${
    mlrngmgex.1 $e |- H = ( 2nd ` R ) $.
    ${
      mlrngmgex.2 $e |- G = ( 1st ` R ) $.
      $( In a unital ring the domain of the first variable of the addition
         equals the domain of the first variable of the multiplication. $)
      rnplrnml0 $p |- ( R e. Ring -> dom dom G = dom dom H ) $=
        ( cring wcel crn cxp wf cdm wceq eqid ringsm cgr wfo wi ringgrp grpfo
        fof fdm wa eqtr dmeqd expcom eqcoms syl com12 3syl mpd ) AFGZBHZULIZULC
        JZBKZKCKZKLZABCULEDULMZNUKBOGUMULBPZUNUQQZABERBULURSUSUMULBJUOUMLZUTUMU
        LBTUMULBUAUNVAUQUNUPUMLVAUQQZUMULCUAVBUMUPVAUMUPLZUQVAVCUBUOUPUOUMUPUCU
        DUEUFUGUHUIUIUJ $.
        $( [24-Jan-2010] $) $( [24-Jan-2010] $)

      $( In a unital ring the range of the addition equals the domain of the
         first variable of the multiplication. $)
      rnplrnml2 $p |- ( R e. Ring -> ran G = dom dom H ) $=
        ( cring wcel crn cdm cgr wceq ringgrp grprndm syl rnplrnml0 eqtrd ) AFG
        ZBHZBIIZCIIQBJGRSKABELBMNABCDEOP $.
        $( [24-Jan-2010] $) $( [24-Jan-2010] $)

      ${ $d G x y z $. $d H x y z $.
        $( In a unital ring the range of the addition equals the range of the
           multiplication. $)
        rnplrnml $p |- ( R e. Ring -> ran G = ran H ) $=
          ( vx vy vz cring wcel crn cxp wf cv co wceq wa wral wrex rngmgmbs4
          eqid ringsm cabl w3a ringi simprr syl sylanc eqcomd ) AIJZCKZBKZULULL
          ULCMZFNZGNZCOUNPUOUNCOZUNPQFULRGULSZUKULPUJFGCULTABCULEDULUAZUBUJBUCJ
          UMQZUPHNZCOUOUNUTCOZCOPUOUNUTBOCOUPUOUTCOZBOPUOUNBOUTCOVBVABOPUDHULRF
          ULRGULRZUQQQUQGFHABCULEDURUEUSVCUQUFUGUHUI $.
          $( [24-Jan-2010] $) $( [24-Jan-2010] $)
      $}

    $}

    ${
    $( In a unital ring the range of the multiplication equals the domain of
       the first variable. $)
    rngdmdmrn $p |- ( R e. Ring -> dom dom H = ran H ) $=
      ( cring wcel c1st cfv crn cdm eqid rnplrnml2 rnplrnml eqtr3d ) ADEAFGZHBI
      IBHANBCNJZKANBCOLM $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
    $}
  $}

  ${
    rnplrnml3.1 $e |- G = ( 1st ` R ) $.
    $( In a unital ring the domain of the first operand of the addition equals
       the domain of the second operand of the addition. $)
    rnplrnml3 $p |- ( R e. Ring -> dom dom G = ran dom G ) $=
      ( cgr wcel cdm crn wceq cring cxp wfo wf wi eqid grpfo fof fdm c0 wne
      dmeq dmxpid syl6eq eqtr ex rnxp syl5com rneq syl5 expcom eqcoms syl6
      com13 mpcom grpn0 syl 3syl ringgrp sylc ) BDEZUSBFZFZUTGZHZAIEUSBGZVDJZVD
      BKVEVDBLZUSVCMZBVDVDNZOVEVDBPVFUTVEHZVGVEVDBQVIVDRSZVCUSVAVDHZVIVJVCMVIVA
      VEFVDUTVETVDUAUBVJVIVKVCVJVIVBVDHZVKVCMZVJVBVEGZHZVLVIVOVNVDHZVLVJVOVPVLV
      BVNVDUCUDVDVDUEUFUTVEUGUHVMVDVBVKVDVBHVCVAVDVBUCUIUJUKULUMBVDVHUNUHUOUPAB
      CUQZVQUR $.
      $( [18-Feb-2010] $) $( [11-Feb-2010] $)
  $}

  ${
    unmnd.1 $e |- H = ( 2nd ` R ) $.
    ${
      $d H w x y $. $d H x y z $. $d R w x y $. $d R x y z $.
      $( In a unital ring the multiplication is a monoid. $)
      unmnd $p |- ( R e. Ring -> H e. Mnd ) $=
        ( vx vy vz vw cring wcel cdm cxp wf cv co wceq wral wa wrex w3a cmnd
        c1st cfv crn eqid ringsm wi ringass expcom 3exp com34 imp impcom
        r19.21aiv ex r19.21aivv cabl ringi pm3.27d 3jca wb rnplrnml2 xpid11
        biimpri feq23 mpancom eleq2 imbi1d ralbidv2 imbi12d anbi12d rexbidv2
        3anbi123d eqcoms syl mpbird c2nd cvv fvex eleq1 mpbiri ismnd1 ax-mp
        sylibr ) AHIZBJJZWEKZWEBLZDMZEMZBNZFMZBNWHWIWKBNBNOZFWEPZEWEPZDWEPZWIWH
        BNWIOWJWIOQZEWEPZDWERZSZBTIZWDWSAUAUBZUCZXBKZXBBLZWLFXBPZEXBPZDXBPZWPEX
        BPZDXBRZSZWDXDXGXIAXABXBXAUDZCXBUDZUEWDXEDEXBXBWDWHXBIZWIXBIZQZXEWDXOQW
        LFXBXOWDWKXBIZWLUFZXMXNWDXQUFXMXNXPWDWLXMXNXPWDWLUFWDXMXNXPSWLWHWIWKAXA
        BXBXKCXLUGUHUIUJUKULUMUNUOWDWJGMZBNWHWIXRBNZBNOWHWIXRXANBNWJWHXRBNZXANO
        WHWIXANXRBNXTXSXANOSGXBPEXBPDXBPZXIWDXAUPIXDQYAXIQDEGAXABXBXKCXLUQURURU
        SWDXBWEOWSXJUTZAXABCXKVAYBWEXBWEXBOZWGXDWOXGWRXIWFXCOZYCWGXDUTYDYCWEXBV
        BVCWFWEXCXBBVDVEYCWNXFDWEXBYCWHWEIZXMWNXFWEXBWHVFZYCWMXEEWEXBYCWIWEIZXN
        WMXEWEXBWIVFZYCWLWLFWEXBYCWKWEIXPWLWEXBWKVFVGVHVIVHVIVHYCWQXHDWEXBYCYEX
        MWQXHYFYCWPWPEWEXBYCYGXNWPYHVGVHVJVKVLVMVNVOBAVPUBZOZWTWSUTZCYJBVQIZYKY
        JYLYIVQIAVPVRBYIVQVSVTDEFVQBWEWEUDWAVNWBWC $.
        $( [24-Jan-2010] $) $( [24-Jan-2010] $)
    $}
  $}

  ${
   fora1.1 $e |- G e. A $.
   $( In a unital ring the addition is an abelian group. $)
   fora1 $p |- ( <. G , H >. e. Ring -> G e. Abel ) $=
     ( cop c1st cfv elisseti op1st eqcomi ringabl ) BCEZBLFGBBCBADHIJK $.
     $( [7-Sep-2009] $) $( [31-Aug-2009] $)
  $}

  ${
   $( In a unital ring the addition is an abelian group. $)
   fora2 $p |- ( ( G e. A /\ <. G , H >. e. Ring ) -> G e. Abel ) $=
     ( wcel cop cring cabl cvv wi elisset csn cif wceq opeq1 eleq1d eleq1
     imbi12d snex elimel fora1 dedth syl imp ) BADZBCEZFDZBGDZUDBHDZUFUGIZBAJUH
     UIUHBAAEAEZKZLZCEZFDZULGDZIBUKBULMZUFUNUGUOUPUEUMFBULCNOBULGPQHULCBUKHUJRS
     TUAUBUC $.
     $( [7-Sep-2009] $) $( [31-Aug-2009] $)
  $}

  $( In a unital ring the addition is an abelian group. $)
  fora $p |- ( <. G , H >. e. Ring -> G e. Abel ) $=
    ( cvv wcel cop cring cabl wbr df-br relrng brrelexi sylbir fora2 mpancom )
    ACDZABEFDZAGDPABFHOABFIABFJKLCABMN $.
    $( [7-Sep-2009] $) $( [31-Aug-2009] $)

  ${

    ununr.1 $e |- H = ( 2nd ` R ) $.
    ununr.2 $e |- X = ran ( 1st ` R ) $.
    ${
      $d H a x y $. $d H x y z $. $d R x y z $. $d X a x y $. $d X x y z $.
      $( The unit of a unital ring is unique. $)
      ununr $p |- ( R e. Ring -> E! x e. X
        A. y e. X ( ( y H x ) = y /\ ( x H y ) = y ) ) $=
        ( va vz cring wcel cv co wceq wa wral wrex weq wi wreu c1st cfv w3a
        cabl cxp wf eqid ringi pm3.27d opreq1 id eqeq12d opreq2 anbi12d rcla4v
        eqeq1 biimpd adantl com12 adantr syl6 com13 com24 imp4b rgen2a jctir
        eqeq1d ralbidv reu4 sylibr ) CJKZBLZALZDMZVLNZVMVLDMZVLNZOZBEPZAEQZVSVL
        HLZDMZVLNZWAVLDMZVLNZOZBEPZOAHRZSZHEPAEPZOVSAETVKVTWJVKVPILZDMVMVLWKDMZ
        DMNVMVLWKCUAUBZMDMVPVMWKDMZWMMNVMVLWMMWKDMWNWLWMMNUCIEPBEPAEPZVTVKWMUDK
        EEUEEDUFOWOVTOABICWMDEWMUGFGUHUIUIWIAHEVMEKZWAEKZVSWGWHWPWGVSWQWHWPWGVM
        WADMZVMNZWAVMDMZVMNZOZVSWQWHSSWFXBBVMEBARZWCWSWEXAXCWBWRVLVMVLVMWADUJXC
        UKZULXCWDWTVLVMVLVMWADUMXDULUNUOWQVSXBWHWQVSWTWANZWRWANZOZXBWHSZVRXGBWA
        EBHRZVOXEVQXFXIVNWTVLWAVLWAVMDUJXIUKZULXIVPWRVLWAVLWAVMDUMXJULUNUOXEXHX
        FXBXEWHXAXEWHSWSXAXEWHWTVMWAUPUQURUSUTVAVBVAVCVDVEVFVSWGAHEWHVRWFBEWHVO
        WCVQWEWHVNWBVLVMWAVLDUMVGWHVPWDVLVMWAVLDUJVGUNVHVIVJ $.
        $( [3-Aug-2009] $) $( [12-Jul-2009] $)
    $}

    ${
      uridm.2 $e |- U = ( Id ` H ) $.
      $( The unit of a ring is an identity element for the multiplication. $)
      uridm $p |- ( ( R e. Ring /\ A e. X ) ->
        ( ( U H A ) = A /\ ( A H U ) = A ) ) $=
        ( cring wcel co wceq wa wi crn cmnd cmagm cexid cin unmnd mndmgmid
        eqid cmpidelt ex 3syl c1st cfv wb rnplrnml eqtr pm3.26 eleq2d imbi1d
        syl mpan mpcom mpbird imp ) BIJZAEJZCADKALACDKALMZUSUTVANZADOZJZVANZUSD
        PJDQRSJZVEBDFTDUAVFVDVAACDVCVCUBHUCUDUEBUFUGZOZVCLZUSVBVEUHZBVGDFVGUBUI
        EVHLZVIUSVJNZGVKVIMEVCLZVLEVHVCUJVMUSVJVMUSMZUTVDVAVNEVCAVMUSUKULUMUDUN
        UOUPUQUR $.
        $( [18-Feb-2010] $) $( [18-Feb-2010] $)

      $( The unit of a ring is an identity element for the multiplication. $)
      uridm1 $p |- ( ( R e. Ring /\ A e. X ) -> ( U H A ) = A ) $=
        ( cring wcel wa co wceq uridm pm3.26d ) BIJAEJKCADLAMACDLAMABCDEFGHNO
        $.
        $( [18-Apr-2010] $) $( [18-Apr-2010] $)

      $( The unit of a ring is an identity element for the multiplication. $)
      uridm2 $p |- ( ( R e. Ring /\ A e. X ) -> ( A H U ) = A ) $=
        ( cring wcel wa co wceq uridm pm3.27d ) BIJAEJKCADLAMACDLAMABCDEFGHNO
        $.
        $( [18-Apr-2010] $) $( [18-Apr-2010] $)
   $}

  $}

  ${
   zerab.1 $e |- Z = ( Id ` G ) $.
   zerab.2 $e |- X = ran G $.
   zerab.3 $e |- G = ( 1st ` R ) $.
   zerab.4 $e |- H = ( 2nd ` R ) $.
   $( The zero of a unital ring is a left absorbing element. $)
   zerab $p |- ( ( R e. Ring /\ A e. X ) -> ( Z H A ) = Z ) $=
     ( cring wcel wa co wceq cgr ringgrp grpidcl grplid mpdan syl adantr
     opreq1d w3a ring0cl pm3.27 3jca ringdir syldan grprid eqcomd ringcl
     pm3.26 syl3anc sylanc 3eqtr3d wb grplcan mpbid ) BKLZAELZMZFADNZVCCNZVCFCN
     ZOZVCFOZVBFFCNZADNZVCVDVEVBVHFADUTVHFOZVAUTCPLZVJBCIQZVKFELZVJFCEHGRFFCEHG
     STUAUBUCUTVAVMVMVAUDVIVDOVBVMVMVAUTVMVABCEFIHGUEUBZVNUTVAUFZUGFFABCDEIJHUH
     UIVKVCELZVCVEOVBVKVPMVEVCVCFCEHGUJUKUTVKVAVLUBZUTVMVAVPVBFABCDEIJHULUTVAUM
     VNVOUNZUOUPVKVPVMVPUDVFVGUQVBVCFVCCEHURVQVBVPVMVPVRVNVRUGUOUS $.
     $( [7-Sep-2009] $) $( [31-Aug-2009] $)

   $( The zero of a unital ring is a right absorbing element. $)
   zerab2 $p |- ( ( R e. Ring /\ A e. X ) -> ( A H Z ) = Z ) $=
     ( cring wcel wa co wceq cgr ringgrp grpidcl grplid mpdan syl adantr
     opreq2d w3a pm3.27 ring0cl 3jca ringdi syldan eqcomd ringcl mpd3an3
     sylanc 3eqtr3d wb grprcan mpbid ) BKLZAELZMZAFDNZVACNZFVACNZOZVAFOZUTAFFCN
     ZDNZVAVBVCUTVFFADURVFFOZUSURCPLZVHBCIQZVIFELZVHFCEHGRFFCEHGSTUAUBUCURUSUSV
     KVKUDVGVBOUTUSVKVKURUSUEURVKUSBCEFIHGUFUBZVLUGAFFBCDEIJHUHUIVIVAELZVAVCOUT
     VIVMMVCVAVAFCEHGSUJURVIUSVJUBZURUSVKVMVLAFBCDEIJHUKULZUMUNVIVMVKVMUDVDVEUO
     UTVAFVACEHUPVNUTVMVKVMVOVLVOUGUMUQ $.
     $( [7-Sep-2009] $) $( [31-Aug-2009] $)
  $}

  ${
   multinv.1 $e |- X = ran G $.
   multinv.2 $e |- G = ( 1st ` R ) $.
   multinv.3 $e |- H = ( 2nd ` R ) $.
   $( Multiplication by an additive inverse. $)
   multinv $p |- (  ( R e. Ring /\ A e. X /\ B e. X )
    ->   ( ( ( inv ` G ) ` A ) H B ) = ( ( inv ` G ) ` ( A H B ) ) ) $=
     ( cring wcel w3a co cgn cfv wceq cgi wa ringdir eqcomd 3simp1 3simp2 cgr
     wi ringgrp eqid grpinvcl ex syl a1dd 3imp 3simp3 3jca sylanc grprinv
     3ad2ant1 opreq1d zerab 3adant2 3eqtrd wb grpinvid1 ringcl syld3an2
     syl3anc mpbird ) CJKZAFKZBFKZLZABEMZDNOZOZAVLOZBEMZVJVMVOPZVKVODMZDQOZPZVJ
     VQAVNDMZBEMZVRBEMZVRVGVHVNFKZVILZVQWAPVJVGWDRWAVQAVNBCDEFHIGSTVGVHVIUAVJVH
     WCVIVGVHVIUBZVGVHVIWCVGVHWCVIVGDUCKZVHWCUDCDHUEZWFVHWCADVLFGVLUFZUGUHUIUJU
     KZVGVHVIULUMUNVJVTVRBEWFVHVTVRPVJAVRDVLFGVRUFZWHUOVGVHWFVIWGUPZWEUNUQVGVIW
     BVRPVHBCDEFVRWJGHIURUSUTWFVKFKVOFKZVPVSVAVJVKVOVRDVLFGWJWHVBWKABCDEFHIGVCV
     GWCVIWLVHVNBCDEFHIGVCWIVDVEVFT $.
     $( [7-Sep-2009] $) $( [2-Sep-2009] $)

   $( Multiplication by an additive inverse. $)
   multinvb $p |- (  ( R e. Ring /\ A e. X /\ B e. X )
     ->   ( A H ( ( inv ` G ) ` B ) ) = ( ( inv ` G ) ` ( A H B ) ) ) $=
     ( cring wcel w3a co cgn cfv wceq cgi cgr eqid grprinv ringgrp 3ad2ant1
     3simp3 sylanc opreq2d ringdi 3simp1 3simp2 wi grpinvcl ex syl a1d 3imp
     3jca zerab2 3adant3 3eqtr3d wb grpinvid1 ringcl syld3an3 syl3anc mpbird
     eqcomd ) CJKZAFKZBFKZLZABEMZDNOZOZABVKOZEMZVIVLVNPZVJVNDMZDQOZPZVIABVMDMZE
     MZAVQEMZVPVQVIVSVQAEDRKZVHVSVQPVIBVQDVKFGVQSZVKSZTVFVGWBVHCDHUAZUBZVFVGVHU
     CZUDUEVFVGVHVMFKZLVTVPPVIABVMCDEFHIGUFVFVGVHUGVIVGVHWHVFVGVHUHWGVFVGVHWHVF
     VHWHUIZVGVFWBWIWEWBVHWHBDVKFGWDUJUKULUMUNZUOUDVFVGWAVQPVHACDEFVQWCGHIUPUQU
     RWBVJFKVNFKZVOVRUSVIVJVNVQDVKFGWCWDUTWFABCDEFHIGVAVFVGWHWKVHAVMCDEFHIGVAWJ
     VBVCVDVE $.
     $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
   mult2inv.1 $e |- X = ran G $.
   mult2inv.2 $e |- G = ( 1st ` R ) $.
   mult2inv.3 $e |- H = ( 2nd ` R ) $.
   $( Multiplication of two additive inverses. $)
   mult2inv $p |- ( ( R e. Ring /\ A e. X /\ B e. X )
     ->   ( ( ( inv ` G ) ` A ) H ( ( inv ` G ) ` B ) ) = ( A H B ) ) $=
     ( cring wcel w3a cgn cfv co wceq multinv cgr eqid grpinvcl ringgrp
     3ad2ant1 3simp3 sylanc syld3an3 multinvb fveq2d grp2inv ringcl 3eqtrd ) CJ
     KZAFKZBFKZLZADMNZNBUONZEOZAUPEOZUONZABEOZUONZUONZUTUKULUPFKZUQUSPUMAUPCDEF
     GHIQDRKZUMVCUNBDUOFGUOSZTUKULVDUMCDHUAUBZUKULUMUCUDUEUNURVAUOABCDEFGHIUFUG
     VDUTFKVBUTPUNUTDUOFGVEUHVFABCDEFHIGUIUDUJ $.
     $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    $d A x $. $d R x $. $d X x $.
    on1el3.1 $e |- G = ( 1st ` R ) $.
    on1el3.2 $e |- X = ran G $.
    $( The only unital ring with a base set consisting in one element
       is the zero ring. $)
    on1el3 $p |- ( ( R e. Ring /\ A e. B ) -> ( X = { A } <-> R =
       <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ) ) $=
      ( vx wcel cring csn wceq cop wb cv wi sneq eqeq2d opeq12 anidms mpancom
      sneqd opeq12d bibi12d imbi2d cxp c1st cfv wf cgr wfo eqid ringgrp crn
      rneqi eqtri grpfo fof 3syl c2nd ringsm w3a cvv wa feq23 visset xpsn feq2
      fconst2 biimpi opex syl6eq syl6bi ax-mp anim12d ex xpid11 syl5ib pm2.43i
      exp3a com13 3imp a1i cdm wrel wss relrng relssdmrn xpss sstri sseli
      jctild eqopi syl6 3expd mpd syl5ibr fveq2 snex op1st eqtr mpan2 rneq
      syl5eq eqcomi rnsnop 3eqtr3g impbid1 vtoclg impcom ) ABICJIZEAKZLZCAAMZAM
      ZKZXPMZLZNZXKEHOZKZLZCXTXTMZXTMZKZYEMZLZNZPXKXSPHABXTALZYHXSXKYIYBXMYGXRY
      IYAXLEXTAQRYIYFXQCYIYEXPYEXPYIYDXOYCXNLZYIYDXOLYIYJXTXTAASTYCXTXNASUAUBZY
      KUCRUDUEXKYBYGXKEEUFZYAYAUFZLZYGYBXKYLECUGUHZUIZYNYGPZXKYOUJIYLEYOUKYPCYO
      YOULUMYOEEDUNZYOUNZGDYOFUOUPUQYLEYOURUSXKYLECUTUHZUIZYPYQPCDYTEFYTULGVAXK
      UUAYPYNYGXKUUAYPYNVBZCVCVCUFZIZYOYELZYTYELZVDZVDYGXKUUBUUGUUDUUBUUGPXKUUA
      YPYNUUGYNYPUUAUUGYNYPUUAUUGYNYPUUAVDUUGPZYNYBUUHYNYNYBUUHYNYBVDZYPUUEUUAU
      UFUUIYPYMYAYOUIZUUEYLEYMYAYOVEYMYCKZLZUUJUUEPXTXTHVFZUUMVGZUULUUJUUKYAYOU
      IZUUEYMUUKYAYOVHUUOYOUUKYAUFZYEUUOYOUUPLUUKXTYOUUMVIVJYCXTXTXTVKZUUMVGZVL
      VMVNVMUUIUUAYMYAYTUIZUUFYLEYMYAYTVEUULUUSUUFPUUNUULUUSUUKYAYTUIZUUFYMUUKY
      AYTVHUUTYTUUPYEUUTYTUUPLUUKXTYTUUMVIVJUURVLVMVNVMVOVPEYAVQZVRVSVTWAWBWCJU
      UCCJJWDZJUNZUFZUUCJWEJUVDWFWGJWHVNUVBUVCWIWJWKWLCYEYEWMWNWOWPWPUVAWQYGYOY
      FUGUHZLZUUEYBCYFUGWRUVFUVEYELUUEYEYEYDWSWTYOUVEYEXAXBUUEYSYEUNEYAYOYEXCEY
      SDYOLZEYSLFUVGYRYSEDYOXCGXDVNXEYCXTUUQUUMXFXGUSXHXIXJ $.
      $( [18-Feb-2010] $) $( [13-Feb-2010] $)

    ${
      $d A x $. $d R x $. $d X x $.
      $( The only unital ring with one element is the zero ring. $)
      on1el4 $p |- ( ( R e. Ring /\ A e. X ) -> ( X ~~ 1o <-> R =
       <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ) ) $=
        ( vx wcel cring c1o cen wbr cop csn wceq wb cv wi opeq12 anidms
        mpancom sneqd opeq12d eqeq2d bibi2d imbi2d wa setwoe on1el3 biimpd ex
        com13 syl pm2.43a com23 imp c1st cfv fveq2 snex op1st eqtr crn rneq
        opex visset rnsnop ensn1 breq1 mpbiri a1d mpan2 sylancr ax-mp com12
        adantr impbid vtoclga impcom ) ADHBIHZDJKLZBAAMZAMZNZWDMZOZPZVTWABGQZWH
        MZWHMZNZWKMZOZPZRVTWGRGADWHAOZWNWGVTWOWMWFWAWOWLWEBWOWKWDWKWDWOWJWCWIWB
        OZWOWJWCOWOWPWHWHAASTWIWHWBASUAUBZWQUCUDUEUFWHDHZVTWNWRVTUGWAWMWRVTWAWM
        RWRWAVTWMWAWRVTWMRZWRWAWRWSRZWRWAUGDWHNZOZWTWHDUHVTWRXBWMVTWRXBWMRVTWRU
        GXBWMWHDBCDEFUIUJUKULUMUKUNUOUPWRWMWARVTWMWRWAWMBUQURZWLUQURZOZWRWARZBW
        LUQUSXEXDWKOZXFWKWKWJUTVAXEXGUGXCWKOZXFXCXDWKVBXHXCVCZWKVCZOZXFXCWKVDCX
        COZXKXFRZEDCVCZOZXNXIOZXMXLXOXPUGDXIOZXMDXNXIVBXQXKXFXQXKUGDXJOZXFDXIXJ
        VBXRXJXAOZXFWIWHWHWHVEGVFZVGXRXSUGXBXFDXJXAVBXBWAWRXBWAXAJKLWHXTVHDXAJK
        VIVJVKUMVLUMUKUMFCXCVDVMVNUMUMVLUMVOVPVQUKVRVS $.
        $( [18-Feb-2010] $) $( [14-Feb-2010] $)
    $}

    ${
      on1el3.3 $e |- Z = ( Id ` G ) $.
      $( The only unital ring with one element is the zero ring. $)
      on1el6 $p |- ( R e. Ring -> ( X ~~ 1o <-> R =
        <. { <. <. Z , Z >. , Z >. } , { <. <. Z , Z >. , Z >. } >. ) ) $=
        ( cring wcel c1o cen wbr cop csn wceq wb ring0cl on1el4 mpdan ) AHIDCIC
        JKLADDMDMNZTMOPABCDEFGQDABCEFRS $.
        $( [18-Feb-2010] $) $( [15-Feb-2010] $)
    $}
  $}

  ${
    $d H u x $. $d X u x $.
    rngunval2.1 $e |- X = ran ( 1st ` R ) $.
    rngunval2.2 $e |- H = ( 2nd ` R ) $.
    rngunval2.3 $e |- U = ( Id ` H ) $.
    $( The value of the unit of a ring. $)
    rngunval2 $p |- ( R e. Ring -> U = U. { u e. X |
     A. x e. X ( ( u H x ) = x /\ ( x H u ) = x ) } ) $=
      ( cring wcel cv co wceq wa crn wral crab cuni cvv c2nd cfv a1i fvex
      syl6eqel eqid idrval syl c1st rnplrnml syl5eq rabeq raleq1 rabbisdv
      eqtrd unieqd eqtr4d ) CJKZDBLZALZEMUTNUTUSEMUTNOZAEPZQZBVBRZSZVAAFQZBFRZS
      URETKDVENURECUAUBZTEVHNURHUCCUAUDUEABTDEVBVBUFIUGUHURVGVDURFVBNZVGVDNURCU
      IUBZPVBFCVJEHVJUFUJGUKVIVGVFBVBRVDVFBFVBULVIVFVCBVBVAAFVBUMUNUOUHUPUQ $.
      $( [18-Feb-2010] $) $( [12-Feb-2010] $)

   $( The unit of a ring belongs to the base set. $)
   ring1cl $p |- ( R e. Ring -> U e. X ) $=
     ( cring wcel c2nd cfv crn cmagm cexid cin wa cmnd unmnd eleq1i mndismgm
     mndisexid jca sylbi syl elin sylibr eqid cgi fveq2i eqtri iorlid c1st
     wceq wb eqtr eleq2d rnplrnml sylancr mpbird ) AHIZBDIZBAJKZLZIZUTVBMNOIZVD
     UTVBMIZVBNIZPZVEUTCQIZVHACFRVIVBQIZVHCVBQFSVJVFVGVBTVBUAUBUCUDVBMNUEUFBVBV
     CVCUGBCUHKVBUHKGCVBUHFUIUJUKUDDAULKZLZUMZVLVCUMZVAVDUNUTVMVNPDVCBDVLVCUOUP
     EAVKVBVBUGVKUGUQURUS $.
     $( [18-Feb-2010] $) $( [12-Feb-2010] $)
  $}

  ${
    $d R x $. $d U x $. $d X x $. $d Z x $.
    uznzr.1 $e |- G = ( 1st ` R ) $.
    uznzr.2 $e |- H = ( 2nd ` R ) $.
    uznzr.3 $e |- Z = ( Id ` G ) $.
    uznzr.4 $e |- U = ( Id ` H ) $.
    uznzr.5 $e |- X = ran G $.
    $( In a unital ring the zero equals the unity iff the ring
       is the zero ring.  $)
    uznzr $p |- ( R e. Ring -> ( X ~~ 1o <-> U = Z ) ) $=
      ( vx cring wcel c1o cen wbr wceq wi ring0cl csn wa crn c1st cfv rneqi
      ring1cl eqcomi eleq2i eleq2 biimpd elsni syl6com sylbi syl setwoe
      syl5com ex com23 mpcom c0 wne rngn0 cv co wral zerab2 r19.21aiva eqtri
      uridm pm3.27d r19.26 eqtr eqcoms imp31 r19.20si eqsn breq1 ensn1g
      syl5bir syl6bir com3l sylbir com24 mpd opreq2 adantr com13 impbid ) AMNZE
      OPQZBFRZFENZWJWKWLSACEFGKITZWMWKWJWLWMWKWJWLSWJEFUAZRZWLWMWKUBWJBCUCZNZWP
      WLSZABDWQCAUDUEZGUFZHJUGWRBENZWSWQEBEWQKUHUIWPXBBWONZWLWPXBXCEWOBUJUKBFUL
      UMUNUOFEUPUQURUSUTEVAVBZWJWLWKSACEGKVCWLWJXDWKWJLVDZBDVEZXEFDVEZRZLEVFZXD
      WKSZWLWJXGFRZLEVFZXIXJSZWJXKLEXEACDEFIKGHVGVHXFXERZLEVFZWJXLXMSWJXNLEWJXE
      ENZUBBXEDVEXERXNXEABDEHEWQWTUCKXAVIJVJVKVHXOXIXLWJXJXOXIXLWJXJSZSZXOXIUBX
      NXHUBZLEVFZXRXNXHLEVLXTXLXQXTXLUBXSXKUBZLEVFZXQXSXKLEVLYBXEFRZLEVFZXQYAYC
      LEXNXHXKYCXHXKYCSZSXEXFXEXFRZXHYEYFXHUBXEXGRZYEXEXFXGVMYGXKYCXEXGFVMURUOU
      RVNVOVPXDYDWJWKXDYDWPWJWKSLEFVQWPWKWOOPQZWJEWOOPVRWJWMYHWNFEVSUOVTWAWBUOW
      CURWCURWDUTWEWLXHLEWLXHXPBFXEDWFWGVHUQWHUTWI $.
      $( [18-Feb-2010] $) $( [14-Feb-2010] $)
  $}


  ${ $d G g h $. $d H g h $. $d x y $.
  $( The predicate "is a division ring". $)
  isdivrng $p |- ( H e. A -> ( <. G , H >. e. DivRing
     <-> ( <. G , H >. e. Ring /\ ( H |` (
       ( ran G \ { ( Id ` G ) } ) X. ( ran G \ { ( Id ` G ) } ) ) )
         e. Grp ) ) ) $=
    ( vx vy vg vh wcel cop cdrng cring crn cgi cfv csn cdif cxp cres cgr wa
    cvv wbr df-br wrel cv copab relopab df-drng releqi mpbir brrelexi sylbir
    anim1i ancoms cabl fora elisset syl ad2antrl pm3.26 jca wceq opeq1 eleq1d
    xpeq12 difeq12 rneq fveq2 sneqd sylanc reseq2 anbi12d opeq2 reseq1
    opelopabg eleq2i syl5bb pm5.21nd ) CAHZBCIZJHZVTKHZCBLZBMNZOZPZWFQZRZSHZTZB
    UAHZVSTZWAVSWLWAWKVSWABCJUBWKBCJUCBCJJUDDUEZEUEZIKHWNWMLWMMNOPZWOQRSHTZDEUF
    ZUDWPDEUGJWQDEUHUIUJUKULUMUNVSWJTWKVSWBWKVSWIWBBUOHWKBCUPBUOUQURUSVSWJUTVAW
    LVTFUEZGUEZIZKHZWSWRLZWRMNZOZPZXEQZRZSHZTZFGUFZHWJWAXIBWSIZKHZWSWGRZSHZTWJF
    GBCUAAWRBVBZXAXLXHXNXOWTXKKWRBWSVCVDXOXGXMSXOXFWGVBZXGXMVBXEWFVBZXQXPXOXEWF
    XEWFVEXBWCVBXDWEVBXQXOXBWCXDWEVFWRBVGXOXCWDWRBMVHVIVJZXRVJXFWGWSVKURVDVLWSC
    VBZXLWBXNWIXSXKVTKWSCBVMVDXSXMWHSWSCWGVNVDVLVOJXJVTFGUHVPVQVR $.
    $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${ $d A u x  $.
    zrdivrng.1 $e |- A e. B $.
    $( The zero ring is not a division ring. $)
    zrdivrng $p |- -.
      <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >.
         e. DivRing $=
      ( vu vx cop csn cvv wcel cdrng wn snex cring crn cgi cfv cdif cxp cres
      cgr wa c0 0ngrp wceq opex elisseti rnsnop grpsn cv co wral crab cuni
      eqcomi eqid grpidval cab elsni adantr elsn biimpri wi wfn wfun cdm df-fn
      funsn dmsnop xpsn eqtr4i mpbir2an snid w3a fnotoprb mpbiri mp3an pm3.27
      opreq1d eqeq1d pm3.26 opreq2d eqeq12d bitrd ex sylbi com12 r19.21aiv jca
      impbii abbii df-rab df-sn 3eqtr4i unieqi unisn eqtri syl6eq sneqd ax-mp
      difeq12i difid xpeq1i xp0r reseq2 res0 eleq1i mtbir a1i intnand isdivrng
      mtbird ) AAFZAFZGZHIZXNXNFZJIZKXMLXOXQXPMIZXNXNNZXNOPZGZQZYBRZSZTIZUAXOYE
      XRYEKXOYEUBTIUCYDUBTYDXNUBSZUBYCUBUDYDYFUDYCUBYBRUBYBUBYBYBAGZYGQUBXSYGYA
      YGXLAAAUEZABCUFZUGZXNTIZYAYGUDAYIUHYKXTAYKXTDUIZEUIZXNUJZYMUDZEYGUKZDYGUL
      ZUMZAEDXTXNYGXSYGYJUNXTUOUPYRYGUMAYQYGYLYGIZYPUAZDUQYLAUDZDUQYQYGYTUUADYT
      UUAYSUUAYPYLAURUSUUAYSYPYSUUADAUTVAUUAYOEYGYMYGIZUUAYOUUBYMAUDZUUAYOVBEAU
      TUUCUUAYOUUCUUAUAZYOAAXNUJZAUDZXNYGYGRZVCZAYGIZUUIUUFUUHXNVDXNVEZUUGUDXNU
      UGVFXLAYHYIVGUUJXLGUUGXLAVHAAYIYIVIVJVKAYIVLZUUKUUHUUIUUIVMUUFXMXNIXMXLAU
      EVLYGYGAAAXNYIVNVOVPUUDYOAYMXNUJZYMUDUUFUUDYNUULYMUUDYLAYMXNUUCUUAVQVRVSU
      UDUULUUEYMAUUDYMAAXNUUCUUAVTZWAUUMWBWCVOWDWEWFWGWHWIWJYPDYGWKDAWLWMWNAYIW
      OWPWQWRWSWTYGXAWPXBYBXCWPYCUBXNXDWSXNXEWPXFXGXHXIHXNXNXJXKWS $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${
    dvrunz.1 $e |- G = ( 1st ` R ) $.
    dvrunz.2 $e |- H = ( 2nd ` R ) $.
    dvrunz.3 $e |- X = ran G $.
    dvrunz.4 $e |- Z = ( Id ` G ) $.
    dvrunz.5 $e |- U = ( Id ` H ) $.
    $( In a division ring the unit is different from the zero. $)
    dvrunz $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cdrng wcel cop csn wn wne cvv cgi cfv fvex eqeltri zrdivrng wceq c1o
      cen wbr cring wb cdif cxp cres cgr drngi pm3.26d uznzr syl wi on1el6
      eleq1 biimpd syl6bi pm2.43a sylbird necon3bd mpi ) ALMZFFNFNOZVHNZLMZPBFQ
      FRFCSTRJCSUAUBUCVGVJBFVGBFUDZEUEUFUGZVJVGAUHMZVLVKUIVGVMDEFOUJZVNUKULUMMA
      CDEFGHIJUNUOZABCDEFGHJKIUPUQVLVGVJVGVLAVIUDZVGVJURVGVMVLVPUIVOACEFGIJUSUQ
      VPVGVJAVILUTVAVBVCVDVEVF $.
      $( [18-Feb-2010] $) $( [14-Feb-2010] $)
  $}

  $c Fld $.

  $( Extend class notation with the class of all fields. $)
  cfld $a class Fld $.

  $( Add alternate definition. Add Kennington's one (=/= the zero ring),
       and Mizar's one: ` ( Id `` G ) =/= ( Id `` H ) ` $)
  $( Definition of a field. A field is a commutative division ring. $)
  df-fld $a |- Fld = ( DivRing i^i Com2 ) $.

  ${ $d G x y $. $d H x y $.
    $( The predicate "is a field". $)
    isfld $p |- ( ( G e. A /\ H e. B ) -> ( <. G , H >. e. Fld <->
      ( <. G , H >. e. DivRing
      /\ A. x e. ran G A. y e. ran G ( x H y ) = ( y H x ) ) ) ) $=
      ( wcel wa cop cfld cdrng ccm2 cin cv co wceq crn wral df-fld a1i eleq2d
      wb elin iscom2 anbi2d 3bitrd ) ECGFDGHZEFIZJGUHKLMZGZUHKGZUHLGZHZUKANZBNZ
      FOUOUNFOPBEQZRAUPRZHUGJUIUHJUIPUGSTUAUJUMUBUGUHKLUCTUGULUQUKCDEFABUDUEUF
      $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    fldi.1 $e |- G = ( 1st ` R ) $.
    fldi.2 $e |- H = ( 2nd ` R ) $.
    fldi.3 $e |- X = ran G $.
    fldi.4 $e |- Z = ( Id ` G ) $.
    ${
      $d G x y z $. $d H x y z $. $d R x y $. $d X x y z $.
      $( The "axioms" of a field. $)
      fldi $p |- ( R e. Fld ->
         (
              ( G e. Abel /\ H : ( X X. X ) --> X )
              /\ (
                 A. x e. X A. y e. X A. z e. X (
                         ( ( x H y ) H z ) = ( x H ( y H z ) )
                        /\ ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) )
                        /\ ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) )
                 /\ E. x e. X A. y e. X (
                         ( y H x ) = y
                        /\ ( x H y ) = y ) )
               /\ (
                  ( H |` ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. Grp
                 /\ A. x e. X A. y e. X ( x H y ) = ( y H x ) ) ) ) $=
        ( cfld wcel cdrng ccm2 cin cabl cxp wf wa cv co wceq w3a wral wrex csn
        cdif cres cgr df-fld eleq2i elin cring wi drngi ringi id 3expia syl
        expdimp com2i syl5 imp sylbi ) DMNDOPQZNZERNGGSGFTUAZAUBZBUBZFUCZCUBZFU
        CVJVKVMFUCZFUCUDVJVKVMEUCFUCVLVJVMFUCZEUCUDVJVKEUCVMFUCVOVNEUCUDUECGUFB
        GUFAGUFVKVJFUCZVKUDVLVKUDUABGUFAGUGUAZFGHUHUIZVRSUJUKNZVLVPUDBGUFAGUFZU
        AZUEZMVGDULUMVHDONZDPNZUAWBDOPUNWCWDWBWCDUONZVSUAZWDWBUPDEFGHIJKLUQWFVT
        WBWDWEVSVTWBWEVIVQUAWAWBUPABCDEFGIJKURVIVQWAWBWBUSUTVAVBDEFGABIJKVCVDVA
        VEVFVF $.
        $( [18-Sep-2010] $) $( [15-Sep-2010] $)
    $}
  $}

  ${
    $d G x y z $. $d R x y z $.
    fldax1.1 $e |- G = ( 1st ` R ) $.
    $( 1st "axiom" of a field. The addition is an abelian group. $)
    fldax1 $p |- ( R e. Fld -> G e. Abel ) $=
      ( vx vy vz cfld wcel cabl crn cxp c2nd cfv wf wa cv co wceq w3a wral
      wrex cgi csn cdif cres cgr eqid fldi 3simp1d pm3.26d ) AGHZBIHZBJZUMKUMAL
      MZNZUKULUOODPZEPZUNQZFPZUNQUPUQUSUNQZUNQRUPUQUSBQUNQURUPUSUNQZBQRUPUQBQUS
      UNQVAUTBQRSFUMTEUMTDUMTUQUPUNQZUQRURUQROEUMTDUMUAOUNUMBUBMZUCUDZVDKUEUFHU
      RVBREUMTDUMTODEFABUNUMVCCUNUGUMUGVCUGUHUIUJ $.
      $( [11-Jul-2010] $) $( [11-Jul-2010] $)
  $}

  ${
    fldax.1 $e |- G = ( 1st ` R ) $.
    fldax.2 $e |- H = ( 2nd ` R ) $.
    fldax.3 $e |- X = ran G $.

    ${
      $d G x y z $. $d H x y z $. $d R x y $. $d X x y z $.
      $( 2nd "axiom" of a field.  The multiplication is an internal
         operation. $)
      fldax2 $p |- ( R e. Fld -> H : ( X X. X ) --> X ) $=
        ( vx vy vz cfld wcel cabl cxp wf wa cv co wceq w3a wral wrex cgi cfv
        csn cdif cres cgr eqid fldi 3simp1d pm3.27d ) AKLZBMLZDDNDCOZUMUNUOPHQZ
        IQZCRZJQZCRUPUQUSCRZCRSUPUQUSBRCRURUPUSCRZBRSUPUQBRUSCRVAUTBRSTJDUAIDUA
        HDUAUQUPCRZUQSURUQSPIDUAHDUBPCDBUCUDZUEUFZVDNUGUHLURVBSIDUAHDUAPHIJABCD
        VCEFGVCUIUJUKUL $.
        $( [11-Jul-2010] $) $( [11-Jul-2010] $)
    $}

    ${
      $d G x y z $. $d H x y z $. $d R x y $. $d X x y z $.
      $( 3rd "axiom" of a field. The multiplication is associative. $)
      fldax3 $p |- ( R e. Fld ->  A. x e. X A. y e. X A. z e. X
                         ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $=
        ( cfld wcel cv co wceq w3a wral wa wrex cabl cxp wf cgi cfv csn cdif
        cres cgr eqid fldi 3simp2d 3simp1 r19.20si adantr syl ) DKLZAMZBMZFNZCM
        ZFNUQURUTFNZFNOZUQURUTENFNUSUQUTFNZENOZUQURENUTFNVCVAENOZPZCGQZBGQZAGQZ
        URUQFNZUROUSURORBGQAGSZRZVBCGQZBGQZAGQZUPETLGGUAGFUBRVLFGEUCUDZUEUFZVQU
        AUGUHLUSVJOBGQAGQRABCDEFGVPHIJVPUIUJUKVIVOVKVHVNAGVGVMBGVFVBCGVBVDVEULU
        MUMUMUNUO $.
        $( [11-Jul-2010] $) $( [11-Jul-2010] $)
    $}


    ${
      $d G x y z $. $d H x y z $. $d R x y $. $d X x y z $.
      $( 4th "axiom" of a field. The multiplication is distributive. $)
      fldax4 $p |- ( R e. Fld ->  A. x e. X A. y e. X A. z e. X
                        ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $=
        ( cfld wcel cv co wceq w3a wral wa wrex cabl cxp wf cgi cfv csn cdif
        cres cgr eqid fldi 3simp2d 3simp2 r19.20si adantr syl ) DKLZAMZBMZFNZCM
        ZFNUQURUTFNZFNOZUQURUTENFNUSUQUTFNZENOZUQURENUTFNVCVAENOZPZCGQZBGQZAGQZ
        URUQFNZUROUSURORBGQAGSZRZVDCGQZBGQZAGQZUPETLGGUAGFUBRVLFGEUCUDZUEUFZVQU
        AUGUHLUSVJOBGQAGQRABCDEFGVPHIJVPUIUJUKVIVOVKVHVNAGVGVMBGVFVDCGVBVDVEULU
        MUMUMUNUO $.
        $( [11-Jul-2010] $) $( [11-Jul-2010] $)
    $}


    ${
      $d G x y z $. $d H x y z $. $d R x y $. $d X x y z $.
      $( 5th "axiom" of a field. Existence of a neutral element. $)
      fldax5 $p |- ( R e. Fld -> E. x e. X A. y e. X ( y H x ) = y ) $=
        ( vz cfld wcel cv co wceq wa wral wrex w3a cabl cxp wf cgi cfv csn
        cdif cres cgr eqid fldi 3simp2d pm3.27d pm3.26 r19.20si r19.22si syl )
        CKLZBMZAMZENZUROZUSURENZUROZPZBFQZAFRZVABFQZAFRUQVBJMZENUSURVHENZENOUSU
        RVHDNENVBUSVHENZDNOUSURDNVHENVJVIDNOSJFQBFQAFQZVFUQDTLFFUAFEUBPVKVFPEFD
        UCUDZUEUFZVMUAUGUHLVBUTOBFQAFQPABJCDEFVLGHIVLUIUJUKULVEVGAFVDVABFVAVCUM
        UNUOUP $.
        $( [11-Jul-2010] $) $( [11-Jul-2010] $)
    $}

    ${
      $d G x y z $. $d H x y z $. $d R x y $. $d X x y z $.
      fldax.4 $e |- Z = ( Id ` G ) $.
      $( 6th "axiom" of a field. The multiplication is a group on the
         underlying set deprived from zero. $)
      fldax6 $p |- ( R e. Fld -> ( H |`
        ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. Grp ) $=
        ( vx vy vz cfld wcel csn cdif cxp cres cgr cv co wceq wral cabl wf wa
        w3a wrex fldi 3simp3d pm3.26d ) AMNZCDEOPZUMQRSNZJTZKTZCUAZUPUOCUAZUBKD
        UCJDUCZULBUDNDDQDCUEUFUQLTZCUAUOUPUTCUAZCUAUBUOUPUTBUACUAUQUOUTCUAZBUAU
        BUOUPBUAUTCUAVBVABUAUBUGLDUCKDUCJDUCURUPUBUQUPUBUFKDUCJDUHUFUNUSUFJKLAB
        CDEFGHIUIUJUK $.
        $( [11-Jul-2010] $) $( [11-Jul-2010] $)
    $}

    ${
      $d G x y z $. $d H x y z $. $d R x y $. $d X x y z $.
      $( 7th "axiom" of a field. The multiplication is commutative. $)
      fldax7 $p |- ( R e. Fld
        -> A. x e. X A. y e. X ( x H y ) = ( y H x ) ) $=
        ( vz cfld wcel cgi cfv csn cdif cxp cres cgr cv co wceq wral cabl wf
        wa w3a wrex eqid fldi 3simp3d pm3.27d ) CKLZEFDMNZOPZUOQRSLZATZBTZEUAZU
        RUQEUAZUBBFUCAFUCZUMDUDLFFQFEUEUFUSJTZEUAUQURVBEUAZEUAUBUQURVBDUAEUAUSU
        QVBEUAZDUAUBUQURDUAVBEUAVDVCDUAUBUGJFUCBFUCAFUCUTURUBUSURUBUFBFUCAFUHUF
        UPVAUFABJCDEFUNGHIUNUIUJUKUL $.
        $( [11-Jul-2010] $) $( [11-Jul-2010] $)
    $}

  $}

  ${
    zrfld.1 $e |- A e. B $.
    $( The zero ring is not a field. $)
    zrfld $p |- -. <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >.
      e. Fld $=
      ( cop csn cfld wcel cdrng ccm2 cin wa zrdivrng intnanr elin mtbir df-fld
      eleq2i ) AADADEZRDZFGSHIJZGZUASHGZSIGZKUBUCABCLMSHINOFTSPQO $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${
    zerdivemp.1 $e |- G = ( 1st ` R ) $.
    zerdivemp.2 $e |- H = ( 2nd ` R ) $.
    zerdivemp.3 $e |- Z = ( Id ` G ) $.
    zerdivemp.4 $e |- X = ran G $.
    zerdivemp.5 $e |- U = ( Id ` H ) $.
    ${
      $d A a $. $d B a $. $d H a $. $d R a $. $d X a $. $d Z a $.
      $( In a unitary ring a left invertible element is not a zero divisor.
          $)
      zerdivemp1 $p |- ( ( R e. Ring /\ A e. ( X \ Z ) /\
      E. a e. X ( a H A ) = U ) ->
         ( B e. X -> ( ( A H B ) = Z -> B = Z ) ) ) $=
        ( cring wcel cdif cv co wceq wrex wi w3a wa 3simpl1 3simpr1 eldifi
        3ad2ant3 adantl 3simpl3 3jca jca ringass eqtr ex opreq1 3simp1 3simp2
        3simp3 3eqtr3d a1d 3exp com14 com13 zerab2 3adant3 crn c1st cfv rneqi
        eqtri uridm1 3adant2 sylc com15 com24 syl eqcoms com25 com12 3imp syl6
        3imp1 3syl pm2.43i 3exp1 opreq2 syl5com r19.23aiv ) COPZAGHQPZIRZAFSZDT
        ZIGUAZBGPZABFSZHTZBHTZUBUBZWOWKWJWTWNWKWJWTUBZUBIGWLGPZWNWKXAWRWJWPXBWN
        WKUCZWSWJWLWQFSZWLHFSZTZWPXCWSUBUBWRWJXFWPXCWSWJXFWPUCZXCUDZWSXHWJXBAGP
        ZWPUCZUDWMBFSZXDTZXHWSUBXHWJXJWJXFWPXCUEXHXBXIWPXGXBWNWKUFXCXIXGWKXBXIW
        NAGHUGUHUIWJXFWPXCUJUKULWLABCEFGJKMUMXHXLWSWJXFWPXCXLWSUBXLXFWPXCWJWSXL
        XFXKXETZWPXCWJWSUBZUBUBXLXFXMXKXDXEUNUOXCWPXMXNXBWNWKWPXMXNUBUBZWNXBWKX
        OUBZWNXKDBFSZTZXBXPUBWMDBFUPXRXMWKWPXBXNXMWKWPXBXNUBUBUBZUBXQXKXQXKTZXM
        XSXTXMUDXQXETZXSXQXKXEUNYAXBWPWKXNWJXBWPWKYAWSWJXBWPWKYAWSUBZUBZXEHTZXQ
        BTZYCWJXBWPUCWKYEYDYBYAYEYDWKWSYAYEYDWKWSUBYAYEYDUCZWSWKYFXQXEBHYAYEYDU
        QYAYEYDURYAYEYDUSUTVAVBVCVDWJXBYDWPWLCEFGHLMJKVEVFWJWPYEXBBCDFGKGEVGCVH
        VIZVGMEYGJVJVKNVLVMVNVBVOVPVQUOVRVSVQVTWAVDWBVOWCVTWDWEWFWQHWLFWGWHVCVB
        WIVDWA $.
        $( [18-Apr-2010] $) $( [18-Apr-2010] $)
    $}

    ${
      $d A a $. $d R a $. $d U a $. $d X a $. $d Z a $.
      $( In a unitary ring a left invertible element is different from
         zero iff ` 1 =/= 0 ` . $)
      rngridfz $p |- ( ( R e. Ring /\ A e. X /\
         E. a e. X ( a H A ) = U ) -> ( A =/= Z <-> U =/= Z ) ) $=
        ( cring wcel cv co wceq wrex w3a wb wi wa eqeq12 biimpd ex zerab2
        syl5com com3l imp 3adant3 opreq2 syl5 crn c1st cfv rneqi eqtri uridm2
        sylanc expcom 3ad2ant3 impbid 3exp1 r19.23aiv com13 3imp necon3bid ) BN
        OZAFOZHPZAEQZCRZHFSZTAGCGVIVJVNAGRZCGRZUAZVNVJVIVQVMVJVIVQUBUBHFVKFOZVM
        VJVIVQVRVMVJTZVIUCZVOVPVTVLVKGEQZRZVPVOVSVIWBVPUBZVRVMVIWCUBZVJVRVMWDVI
        VRVMWCVIVRVMWCUBVMWAGRZWCVIVRUCVMWEWCVMWEUCWBVPVLCWAGUDUEUFVKBDEFGKLIJU
        GUHUFUIUJUKUJAGVKEULUMVTACEQZAGEQZRZVOVPVSVIWHVOUBZVJVRVIWIUBVMVIVJWIWF
        ARZWGGRZWIVIVJUCWJWKUCWHVOWFAWGGUDUEABCEFJFDUNBUOUPZUNLDWLIUQURMUSABDEF
        GKLIJUGUTVAVBUJCGAEULUMVCVDVEVFVGVH $.
        $( [18-Apr-2010] $) $( [18-Apr-2010] $)
    $}
  $}

  ${
    $d x y $.
    $( ` ZZ ` is a commutative ring. $)
    zintdom $p |- <. + , x. >. e. ( Ring i^i Com2 ) $=
      ( vx vy caddc cmul cop cring ccm2 cin wcel elin cnring cv co wceq crn
      wral cc axmulcom cxp wfo cabl cnaddabl cgr cmnd ablgrp grpmnd cmagm
      cexid mndmgmid wf cdm axaddopr fdm dmeq wi dmxpid eqtr expcom eqcoms
      com12 ax-mp 3syl opidon syl forn eleq2i syl2anb rgen2a cvv wb addex
      mulex iscom2 mp2an mpbir mpbir2an ) CDEZFGHIVQFIVQGIZVQFGJKVRALZBLZDMVTVS
      DMNZBCOZPAWBPZWAABWBVSQIVTQIWAVSWBIVTWBIVSVTRWBQVSQQSZQCTZWBQNCUAIZWEUBWF
      CUCICUDIZWECUECUFWGCUGUHHIWECUICQWDQCUJZQCUKZUKZNZULWHWIWDNWJWDUKZNZWKWDQ
      CUMWIWDUNWLQNWMWKUOZQUPWNQWLWMQWLNZWKWOWKUOWLWJWOWLWJNWKQWLWJUQURUSUTUSVA
      VBVAVCVDVBVAWDQCVEVAZVFWBQVTWPVFVGVHCVIIDVIIVRWCVJVKVLVIVICDABVMVNVOVP $.
      $( [18-Apr-2010] $) $( [18-Apr-2010] $)
  $}


