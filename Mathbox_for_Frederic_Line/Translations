$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Translations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    trfun.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trinv.1 $e |- X = ran G $.
    ${
      $d A x $.  $d G x $.  $d X x $.
      $( The domain of a right translation.  The term ` A ` is a constant:
         ` x ` is not present. $)
      trdom2 $p |- ( ( G e. GrpOp /\ A e. X ) -> dom F = X ) $=
        ( cgr wcel wa cv co cvv wral cdm wceq oprex a1i r19.21aiva cmpdom
        sylib ) DHIBEIJZAKZBDLZMIZAENCOEPUBUEAEUEUBUCEIJUCBDQRSAEUDCFTUA $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d X x $.
      $( A right translation is a set. $)
      trset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( wfun cdm cvv wcel cgr funex cv co cmpfun unsgrp cmpdom2 syl5eqel
        sylancr ) CHCIZJKCJKDLKZJCMAEANZBDOCFPUBEJUADEGQAEUCBCDFRST $.
        $( [20-Sep-2010] $) $( [19-Sep-2010] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( The range of a right translation.  The term ` A ` is a constant: ` x `
         is not present. $)
      trran2 $p |- ( ( G e. GrpOp /\ A e. X ) -> ran F = X ) $=
        ( vy cgr wcel wa cv co wceq wrex cab crn wi eleq1 grpocl 3exp com23
        imp impcom syl5cbir expcom r19.23adv cgn cfv simpll simpr eqid
        grpoinvcl adantr syl111anc cgi grpolinv eqcomd opreq2d grporid adantlr
        simplr grpoass syl13anc 3eqtr4d opreq1 eqeq2d rcla4ev syl11anc ex
        impbid abbi1dv cmpran syl5eq ) DIJZBEJZKZHLZALZBDMZNZAEOZHPECQVQWBHEVQW
        BVREJZVQWAWCAEVSEJZVQWAWCRWAWCVTEJZWDVQKVRVTESVQWDWEVOVPWDWERVOWDVPWEVO
        WDVPWEVSBDEGTUAUBUCUDUEUFUGVQWCWBVQWCKZVRBDUHUIZUIZDMZEJZVRWIBDMZNZWBWF
        VOWCWHEJZWJVOVPWCUJZVQWCUKZVQWMWCBDWGEGWGULZUMUNZVRWHDEGTUOWFVRDUPUIZDM
        ZVRWHBDMZDMZVRWKWFWRWTVRDWFWTWRVQWTWRNWCBWRDWGEGWRULZWPUQUNURUSVOWCVRWS
        NVPVOWCKWSVRVRWRDEGXBUTURVAWFVOWCWMVPWKXANWNWOWQVOVPWCVBVRWHBDEGVCVDVEW
        AWLAWIEVSWINVTWKVRVSWIBDVFVGVHVIVJVKVLAHEVTCFVMVN $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.  $d G x y z $.  $d X x y z $.
      $( A right translation is a bijection.  The term ` A ` is a constant. $)
      trooo $p |- ( ( G e. GrpOp /\ A e. X ) -> F : X -1-1-onto-> X ) $=
        ( vy vz cgr wcel wa wfn crn wceq cv cfv wi wral w3a wf1o co cvv oprex
        a1i rgen wb fopab2ga mpbii trran2 fvopab2b mpan2 ad2antrl simpr jctir
        adantl opreq1 fvmptg syl eqeq12d biimpd grporcan 3exp2 imp3a com23
        imp31 sylibd ex r19.21aivv 3jca cmpt hbmpt1 ax-17 dff1o6f f1oeq1 ax-mp
        fneq1i rneqi eqeq1i fveq1i eqeq12i imbi1i 2ralbii 3anbi123i 3bitr4i
        sylibr ) DJKZBEKZLZCEMZCNZEOZAPZCQZHPZCQZOZWMWOOZRZHESAESZTZEECUAZWIWJW
        LWTWIWMBDUBZUCKZAESZWJXDAEXDWMEKZWMBDUDZUEUFXEWJUGWIAEXCCFUHUEUIABCDEFG
        UJWIWSAHEEWIXFWOEKZLZWSWIXILZWQXCWOBDUBZOZWRXJWQXLXJWNXCWPXKXFWNXCOZWIX
        HXFXDXMXGAEXCUCCFUKULUMXJXHXKUCKZLZWPXKOXIXOWIXIXHXNXFXHUNWOBDUDUOUPAWO
        XCXKEUCCWMWOBDUQFURUSUTVAWGWHXIXLWRUGZWGXIWHXPWGXFXHWHXPRWGXFXHWHXPWMWO
        BDEGVBVCVDVEVFVGVHVIVJEEAEXCVKZUAZXQEMZXQNZEOZWMXQQZWOXQQZOZWRRZHESAESZ
        TXBXAAHIEEXQAIEXCVLIPXQKHVMVNCXQOXBXRUGFEECXQVOVPWJXSWLYAWTYFECXQFVQWKX
        TECXQFVRVSWSYEAHEEWQYDWRWNYBWPYCWMCXQFVTWOCXQFVTWAWBWCWDWEWF $.
        $( [31-Dec-2010] $) $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.  $d D y z $.  $d G x y z $.  $d X x y z $.
      trinv.2 $e |- D = ( /g ` G ) $.
      $( The converse of a right translation.  The term ` A ` is a constant. $)
      trinv $p |- ( ( G e. GrpOp /\ A e. X )
          -> `' F = ( x e. X |-> ( x D A ) ) ) $=
        ( vy vz cgr wcel wa ccnv cv co cmpt wceq cfv wral wf1o trooo adantr
        grpdivcl 3exp com23 imp31 jca cvv opreq1 cmpbvb fvmptg oprex sylancl
        grpnpcan eqtrd f1ocnvfv sylc eqid fvopab2b simpr eqtr4d r19.21aiva wfn
        wb dff1o4 simprbi syl a1i rgen fopab2ga mpbii hbmpt1 hbcnv eqfnfv2f
        cnveqi fneq1i anbi1i eqeq1i fveq1i ralbii bibi12i 3imtr4i syl11anc
        mpbird ) ELMZBFMZNZDOZAFAPZBCQZRZSZWKWJTZWKWMTZSZAFUAZWIWQAFWIWKFMZNZWO
        WLWPWTFFDUBZWLFMZNWLDTZWKSWOWLSWTXAXBWIXAWSABDEFGHUCZUDWGWHWSXBWGWSWHXB
        WGWSWHXBWKBCEFHIUEUFUGUHZUIWTXCWLBEQZWKXBXFUJMXCXFSWTJWLJPZBEQZXFFUJDXG
        WLBEUKAJFWKBEQZXHDGWKXGBEUKULUMXEWLBEUNUOWGWHWSXFWKSZWGWSWHXJWGWSWHXJWK
        BCEFHIUPUFUGUHUQFFWLWKDURUSWSWLUJMZWPWLSWTAFWLUJWMWMUTZVAWIWSVBWKBCUNZU
        OVCVDWIWJFVEZWMFVEZWNWRVFZWIXAXNXDXADFVEXNFFDVGVHVIWIXKAFUAZXOXKAFXKWSX
        MVJVKXQXOVFWIAFWLWMXLVLVJVMAFXIRZOZFVEZXONXSWMSZWKXSTZWPSZAFUAZVFXNXONX
        PAKFXSWMAKXRAKFXIVNVOAKFWLVNVPXNXTXOFWJXSDXRGVQZVRVSWNYAWRYDWJXSWMYEVTW
        QYCAFWOYBWPWKWJXSYEWAVTWBWCWDWEWF $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A a b x y $.  $d B a b x y $.  $d F x $.  $d G a b x y $.
      $d X a b x y $.
      cmprtr.1 $e |- H = ( x e. X |-> ( x G B ) ) $.
      $( Composite of two right translations.  The terms ` A ` and ` B ` are
         constant.  Don't use.  See ~ cmprtr2 . $)
      cmprtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( x G ( B G A ) ) ) ) $=
        ( vy va vb cgr wcel w3a ccom cv co wceq wa copab cmpt crn wss trran2
        eqimss syl 3adant2 oprex opreq1 df-mpt eqtri ax-17 weq wb eleq1 adantr
        simpr eqeq12d anbi12d cbvopab 3eqtri eqid fopabco simpl1 simpl3 simpl2
        grpoass syl13anc eqeq2d pm5.32da opabbid eqcomi a1i 3eqtrd ) ENOZBGOZCG
        OZPZDFQZARZGOZKRZWBCESZBESZTZUAZAKUBZWCWDWBCBESESZTZUAZAKUBZAGWJUCZVTFU
        DZGUEZWAWITVQVSWPVRVQVSUAWOGTWPACFEGJIUFWOGUGUHUIAKLMKGGWELRZBESZWFFDWI
        WBCEUJWQBEUJWEBEUJWQWEBEUKFAGWEUCWCWDWETUAAKUBJAKGWEULUMDAGWBBESZUCWCWD
        WSTZUAZAKUBWQGOZMRZWRTZUAZLMUBHAKGWSULXAXEAKLMXALUNXAMUNXEAUNXEKUNALUOZ
        KMUOZUAZWCXBWTXDXFWCXBUPXGWBWQGUQURXHWDXCWSWRXFXGUSXFWSWRTXGWBWQBEUKURU
        TVAVBVCWIVDVEUHVTWHWLAKVTAUNVTKUNVTWCWGWKVTWCUAZWFWJWDXIVQWCVSVRWFWJTVQ
        VRVSWCVFVTWCUSVQVRVSWCVGVQVRVSWCVHWBCBEGIVIVJVKVLVMWMWNTVTWNWMAKGWJULVN
        VOVP $.
        $( [17-Oct-2010] $)
    $}

    ${
      $d A x y $.  $d B x y $.  $d F y $.  $d G x y $.  $d X x y $.
      cmprtr2.1 $e |- H = ( x e. X |-> ( x G B ) ) $.
      $( Composite of two right translations.  ( ~ cmprtr with a distinct
         variable condition relaxed.) $)
      cmprtr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( x G ( B G A ) ) ) ) $=
        ( vy cgr wcel w3a ccom cv co cmpt eqid opreq1 cmpbvb eqtri cmprtr
        syl6eq ) ELMBGMCGMNDFOKGKPZCBEQZEQZRZAGAPZUFEQZRKBCDEFGDAGUIBEQZRZKGUEB
        EQZRHAKGUKUMULULSUIUEBETUAUBIFAGUICEQZRZKGUECEQZRJAKGUNUPUOUOSUIUECETUA
        UBUCKAGUGUJUHUHSUEUIUFETUAUD $.
        $( [1-Jan-2011] $)
    $}

    ${
      $d A x $.  $d B a u $.  $d F a u $.  $d G x $.  $d X x $.
      $( The image of a set through a translation. $)
      imtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X ) ->
       ( F " B ) = { a | E. u e. B a = ( F ` u ) } ) $=
        ( cgr wcel cpw w3a cima cv cfv wceq wrex cab wfun cdm wss dfimafn co
        cmpfun wa wi trdom2 pweq eleq2d biimpd eqcoms elpwi syl6 syl 3impia
        sylancr eqcom rexbii abbii syl6eq ) FKLZCGLZDGMZLZNZEDOZBPEQZHPZRZBDSZH
        TZVJVIRZBDSZHTEUADEUBZUCZVHVMRVGBHDEUDAGAPCFUEEIUFVCVDVFVQVCVDUGVPGRZVF
        VQUHACEFGIJUIVRVFDVPMZLZVQVFVTUHGVPGVPRZVFVTWAVEVSDGVPUJUKULUMDVPUNUOUP
        UQURVLVOHVKVNBDVIVJUSUTVAVB $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A a v x $.  $d B a x $.  $d F a x $.  $d G a v x $.  $d X a x $.
      prsubrtr.1 $e |- H = ( cset ` G ) $.
      $( The product of a subset ` B ` of ` X ` by an element of ` X ` is the
         image of ` B ` by a right translation. $)
      prsubrtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X )
        -> ( B H { A } ) = ( F " B ) ) $=
        ( va vv cgr wcel cpw w3a cv co wceq csn wrex cab cfv cima wa c0 wne wb
        snnzg 3ad2ant2 adantr r19.9rzv bicomd syl elsn opreq2 eqeq2d sylbi
        rexbiia syl5bb cvv fvopab2b wi elelpwi expcom 3ad2ant3 imp oprex
        sylancl eqcomd bitrd rexbidva abbidv cdm simp1 crn grprndm syl5eq pweq
        eleq2d biimpa 3adant2 snelpwg adantl mpbid 3adant3 eqid iscst2
        syl111anc imtr 3eqtr4d ) EMNZBGNZCGOZNZPZKQZAQZLQZERZSZLBTZUAZACUAZKUBZ
        WQWRDUCZSZACUAZKUBCXBFRZDCUDWPXDXHKWPXCXGACWPWRCNZUEZXCWQWRBERZSZXGXKXM
        LXBUAZXMXCXKXBUFUGZXNXMUHWPXOXJWMWLXOWOBGUIUJUKXOXMXNXMLXBULUMUNXAXMLXB
        WSXBNWSBSZXAXMUHLBUOXPWTXLWQWSBWREUPUQURUSUTXKXLXFWQXKXFXLWRGNZXLVANXFX
        LSXKAGXLVADHVBWPXJXQWOWLXJXQVCWMXJWOXQWRCGVDVEVFVGWRBEVHVIVJUQVKVLVMWPW
        LCEVNVNZOZNZXBXSNZXIXESWLWMWOVOWLWOXTWMWLWOXTWLWNXSCWLGXRSWNXSSWLEVPXRG
        EVQIVRZGXRVSUNVTWAWBWLWMYAWOWLWMUEBXRNZYAWLWMYCWLGXRBYBVTWAWMYCYAUHWLBX
        RGWCWDWEWFLACXBEFMXRKXRWGJWHWIAABCDEGKHIWJWK $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A a x $.  $d B a $.  $d F a $.  $d G a x $.  $d X a x $.
      prsubrtr2.1 $e |- H = ( cset ` G ) $.
      $( The product of a subset ` B ` of ` X ` by an element of ` X ` is the
         image of ` B ` by a right translation.  ( ~ prsubrtr with a distinct
         variable condition relaxed.) $)
      prsubrtr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X )
        -> ( B H { A } ) = ( F " B ) ) $=
        ( va cv co cmpt eqid opreq1 cmpbvb eqtri prsubrtr ) KBCDEFGDAGALZBEMZNZ
        KGKLZBEMZNHAKGUAUDUBUBOTUCBEPQRIJS $.
        $( [1-Jan-2011] $)
    $}

    ${
      $d A x $.  $d G x $.  $d X x $.
      caytr.1 $e |- H = ( cset ` G ) $.
      $( "It follows that if the entire group is multiplied by any one of the
         symbols, either as further or nearer factor, the effect is simply to
         reproduce the group... ."  Cayley, On the theory of groups, as
         depending on the symbolic equation th^n = 1, 1854.  (it is the
         original paper where the axiomatic definition of a group was given for
         the first time.) $)
      caytr $p |- ( ( G e. GrpOp /\ A e. X ) -> ( X H { A } ) = X ) $=
        ( cgr wcel wa csn co cima crn cpw wceq wss ssid cvv wb unsgrp adantr
        elpwg syl mpbiri prsubrtr2 mpd3an3 wfn wf1o trooo f1ofn fnima trran2
        3eqtrd ) DJKZBFKZLZFBMENZCFOZCPZFUQURFFQKZUTVARUSVCFFSZFTUSFUAKZVCVDUBU
        QVEURDFHUCUDFFUAUEUFUGABFCDEFGHIUHUIUSCFUJZVAVBRUSFFCUKVFABCDFGHULFFCUM
        UFFCUNUFABCDFGHUOUP $.
        $( [15-Oct-2012] $)
    $}

  $}


  ${
    ltrdom.1 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrdom.2 $e |- X = ran G $.
    ${
      $d A x $.  $d G x $.  $d X x $.
      $( The domain of a left translation.  The term ` A ` is a constant. $)
      ltrdom $p |- ( ( G e. GrpOp /\ A e. X ) -> dom F = X ) $=
        ( cgr wcel wa cv co cvv wral cdm wceq oprex a1i r19.21aiva cmpdom
        sylib ) DHIBEIJZBAKZDLZMIZAENCOEPUBUEAEUEUBUCEIJBUCDQRSAEUDCFTUA $.
        $( [26-Apr-2012] $)
    $}

    ${
      $d X x $.
      $( A left translation is a set. $)
      ltrset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( wfun cdm cvv wcel cgr funex cv co cmpfun unsgrp cmpdom2 syl5eqel
        sylancr ) CHCIZJKCJKDLKZJCMAEBANZDOCFPUBEJUADEGQAEBUCCDFRST $.
        $( [28-Apr-2012] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( The range of a left translation.  The term ` A ` is a constant. $)
      ltrran2 $p |- ( ( G e. GrpOp /\ A e. X ) -> ran F = X ) $=
        ( vy cgr wcel wa cv co wceq wrex cab crn wi eleq1 grpocl 3expia impcom
        syl5cbir expcom r19.23adv cgn cfv simpll eqid grpoinvcl adantr simpr
        syl111anc cgi grpolid eqcomd adantlr grporinv opreq1d simplr grpoass
        syl13anc 3eqtrd opreq2 eqeq2d rcla4ev syl11anc ex impbid abbi1dv
        cmpran syl5eq ) DIJZBEJZKZHLZBALZDMZNZAEOZHPECQVOVTHEVOVTVPEJZVOVSWAAEV
        QEJZVOVSWARVSWAVREJZWBVOKVPVRESVOWBWCVMVNWBWCBVQDEGTUAUBUCUDUEVOWAVTVOW
        AKZBDUFUGZUGZVPDMZEJZVPBWGDMZNZVTWDVMWFEJZWAWHVMVNWAUHZVOWKWABDWEEGWEUI
        ZUJUKZVOWAULZWFVPDEGTUMWDVPDUNUGZVPDMZBWFDMZVPDMZWIVMWAVPWQNVNVMWAKWQVP
        VPWPDEGWPUIZUOUPUQWDWPWRVPDVOWPWRNWAVOWRWPBWPDWEEGWTWMURUPUKUSWDVMVNWKW
        AWSWINWLVMVNWAUTWNWOBWFVPDEGVAVBVCVSWJAWGEVQWGNVRWIVPVQWGBDVDVEVFVGVHVI
        VJAHEVRCFVKVL $.
        $( [28-Apr-2012] $)
    $}

    ${
      $d A a b x z $.  $d G a b x z $.  $d X a b x z $.
      $( A left translation is a bijection.  The term ` A ` is a constant. $)
      ltrooo $p |- ( ( G e. GrpOp /\ A e. X ) -> F : X -1-1-onto-> X ) $=
        ( va vb vz cv co cmpt wceq cgr wcel wa wf1o wi f1oeq1 wfn crn cfv weq
        wral w3a cvv oprex a1i rgen eqid mptfng sylib ltrran2 simprl grpocl
        3exp com3r adantr com3l imp31 opreq2 fvmptg syl11anc eqcomd 3adant3
        simp3 simprr 3expia adantld imp jca syl 3eqtrd wb grpolcan expcom
        com34 mpbid r19.21aivv 3jca ax-17 dff1o6f sylibr syl5bir ax-mp ) CAEBAK
        ZDLZMZNZDOPZBEPZQZEECRZSFWJWNEEWIRZWMEECWITWMWIEUAZWIUBENZHKZWIUCZIKZWI
        UCZNZHIUDZSZIEUEHEUEZUFWOWMWPWQXEWMWHUGPZAEUEZWPXGWMXFAEXFWGEPBWGDUHUIU
        JUIAEWHWIWIUKZULUMABWIDEXHGUNWMXDHIEEWMWREPZWTEPZQZXBXCWMXKXBUFZBWRDLZB
        WTDLZNZXCXLXMWSXAXNWMXKXMWSNXBWMXKQZWSXMXPXIXMEPZWSXMNWMXIXJUOWKWLXKXQX
        KWKWLXQXIWKWLXQSSXJWKWLXIXQWKWLXIXQBWRDEGUPUQURUSUTVAAWRWHXMEEWIWGWRBDV
        BXHVCVDVEVFWMXKXBVGXLXJXNEPZQZXAXNNWMXKXSXBXPXJXRWMXIXJVHWMXKXRWMXJXRXI
        WKWLXJXRBWTDEGUPVIVJVKVLVFAWTWHXNEEWIWGWTBDVBXHVCVMVNWMXKXOXCVOZXBWKWLX
        KXTXKWKWLXTXIXJWKWLXTSSXIXJWLWKXTXIXJWLWKXTSWKXIXJWLUFXTWRWTBDEGVPVQUQV
        RVKUTVAVFVSUQVTWAHIJEEWIJKWIPZHWBYAIWBWCWDWEWF $.
        $( [29-Apr-2012] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( Left translation expressed as a composite. $)
      ltrcmp $p |- ( ( G e. GrpOp /\ A e. X ) ->
        F = ( G o. { <. x , y >. | ( x e. X /\ y = <. A , x >. ) } ) ) $=
        ( cgr wcel wa cv cop cxp wral wfn wceq copab ccom opelxpi adantll
        r19.21aiva wfo grpfo fofn syl adantr eqid co cmpt cfv df-mpt df-opr
        eqeq2i anbi2i opabbii 3eqtri fnopabco2b syl11anc ) EIJZCFJZKZCALZMZFFNZ
        JZAFOEVEPZDEVCFJZBLZVDQKABRZSQVBVFAFVAVHVFUTCVCFFTUAUBUTVGVAUTVEFEUCVGE
        FHUDVEFEUEUFUGABFVDVEVJDEVJUHDAFCVCEUIZUJVHVIVKQZKZABRVHVIVDEUKZQZKZABR
        GABFVKULVMVPABVLVOVHVKVNVICVCEUMUNUOUPUQURUS $.
        $( [3-Jul-2012] $)
    $}

    ${
      $d A x y z $.  $d F z $.  $d G x y z $.  $d N x y z $.  $d X x y z $.
      ltrinvlem.2 $e |- D = ( /g ` G ) $.
      ltrinvlem.3 $e |- N = ( inv ` G ) $.
      $( The converse of a left translation.  The term ` A ` is a constant. $)
      ltrinvlem $p |- ( ( G e. GrpOp /\ A e. X )
          -> `' F = ( x e. X |-> ( ( N ` A ) G x ) ) ) $=
        ( vy vz cgr wcel wa ccnv cfv cv co cmpt wceq wral wf1o ltrooo adantr
        simpll grpoinvcl simpr grpocl syl111anc jca simplr opreq2 cmpbvb
        fvmptg syl11anc w3a grpoass eqcomd syl13anc cgi eqid grporinv opreq1d
        grpolid adantlr eqtrd 3eqtrd f1ocnvfv sylc fvopab2b eqtr4d r19.21aiva
        wfn wb f1ocnv f1ofn 3syl syldan syl wal wi hbmpt1 hbcnv a1i cnveq
        eleq2d hbth albid 3imtr4d ax-mp eqfnfv2f mpbird ) ENOZBGOZPZDQZAGBFRZAS
        ZETZUAZUBZWTWRRZWTXBRZUBZAGUCZWQXFAGWQWTGOZPZXDXAXEXIGGDUDZXAGOZPXADRZW
        TUBXDXAUBXIXJXKWQXJXHABDEGHIUEZUFXIWOWSGOZXHXKWOWPXHUGZWQXNXHBEFGIKUHZU
        FZWQXHUIZWSWTEGIUJUKZULXIXLBXAETZBWSETZWTETZWTXIXKXTGOZXLXTUBXSXIWOWPXK
        YCXOWOWPXHUMZXSBXAEGIUJUKLXABLSZETZXTGGDYEXABEUNALGBWTETZYFDHWTYEBEUNUO
        UPUQXIWOWPXNXHXTYBUBXOYDXQXRWOWPXNXHURPYBXTBWSWTEGIUSUTVAXIYBEVBRZWTETZ
        WTXIYAYHWTEWQYAYHUBXHBYHEFGIYHVCZKVDUFVEWOXHYIWTUBWPWTYHEGIYJVFVGVHVIGG
        XAWTDVJVKXIXHXKXEXAUBXRXSAGXAGXBXBVCZVLUQVMVNWQWRGVOZXBGVOZXCXGVPWQXJGG
        WRUDYLXMGGDVQGGWRVRVSWQGGXBUDZYMWOWPXNYNXPAWSXBEGYKIUEVTGGXBVRWAAMGWRXB
        DAGYGUAZUBZMSZWROZYRAWBZWCHYPYQYOQZOZUUAAWBZYRYSUUAUUBWCYPAMYOAMGYGWDWE
        WFYPWRYTYQDYOWGWHZYPYRUUAAYPAHWIUUCWJWKWLAMGXAWDWMUQWN $.
        $( [30-Apr-2012] $)
    $}

    ${
      $d A v x y z $.  $d B v w x y z $.  $d F z $.  $d G v w x y z $.
      $d X v w x y z $.
      cmpltr.2 $e |- H = ( x e. X |-> ( B G x ) ) $.
      $( Composite of two left translations.  The terms ` A ` and ` B ` are
         constant.  Don't use.  See ~ cmpltr2 . $)
      cmpltr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( ( A G B ) G x ) ) ) $=
        ( vv vz vw vy cgr wcel w3a ccom cv co wceq wa copab cmpt crn wss
        ltrran2 eqimss syl 3adant2 oprex opreq2 df-mpt ax-17 weq eleq1 eqeq2d
        anbi12d cbvopab1 3eqtri eqtri eqid fopabco opreq2d syl6eq wb wi
        grpoass eqcomd 3exp2 3imp pm5.32d opabbidv eqcomi a1i 3eqtrd ) EOPZBGPZ
        CGPZQZDFRZASZGPZKSZBCWBETZETZUAZUBZAKUCZWCWDBCETWBETZUAZUBZAKUCZAGWJUDZ
        VTFUEZGUFZWAWIUAVQVSWPVRVQVSUBWOGUAWPACFEGJIUGWOGUHUIUJWPWALSZGPZWDBCWQ
        ETZETZUAZUBZLKUCZWILMANKGGWSBWBETZWTFDXCCWQEUKBWBEUKBWSEUKWBWSBEULFAGWE
        UDWCMSZWEUAZUBZAMUCWRXEWSUAZUBZLMUCJAMGWEUMXGXIAMLXGLUNXIAUNALUOZWCWRXF
        XHWBWQGUPXJWEWSXEWBWQCEULUQURUSUTDAGXDUDWCNSXDUAUBANUCHANGXDUMVAXCVBVCX
        BWHLKAXBAUNWHLUNLAUOZWRWCXAWGWQWBGUPXKWTWFWDXKWSWEBEWQWBCEULVDUQURUSVEU
        IVTWHWLAKVTWCWGWKVQVRVSWCWGWKVFZVGVQVRVSWCXLVQVRVSWCQUBZWFWJWDXMWJWFBCW
        BEGIVHVIUQVJVKVLVMWMWNUAVTWNWMAKGWJUMVNVOVP $.
        $( [2-Jul-2012] $)

    $}

    ${
      $d A x y $.  $d B x y $.  $d G x y $.  $d X x y $.
      cmpltr2.2 $e |- H = ( x e. X |-> ( B G x ) ) $.
      $( Composite of two left translations.  The terms ` A ` and ` B ` are
         constant. $)
      cmpltr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( ( A G B ) G x ) ) ) $=
        ( vy cgr wcel w3a ccom co cv cmpt eqid opreq2 cmpbvb eqtri cmpltr
        syl6eq ) ELMBGMCGMNDFOKGBCEPZKQZEPZRZAGUEAQZEPZRKBCDEFGDAGBUIEPZRZKGBUF
        EPZRHAKGUKUMULULSUIUFBETUAUBIFAGCUIEPZRZKGCUFEPZRJAKGUNUPUOUOSUIUFCETUA
        UBUCKAGUGUJUHUHSUFUIUEETUAUD $.
        $( [2-Jul-2012] $)
    $}
  $}

  ${
    $d A y z $.  $d B y $.  $d F x $.  $d G y $.  $d X x z $.  $d X y $.
    reptertr2.2 $e |- F = ( y e. X |-> ( y G B ) ) $.
    reptertr2.3 $e |- H = ( x e. X |-> A ) $.
    reptertr2.4 $e |- A e. _V $.
    $( Replacement of ` x ` by the term ` A ` in a translation. ` B ` is a
       constant.  Note that ` x ` and ` y ` can be the same. $)
    reptertr2 $p |- ( A. x e. X A e. X
      -> ( F o. H ) = ( x e. X |-> ( A G B ) ) ) $=
      ( vz wcel wral crn wss ccom co cmpt wceq cv wa copab df-mpt eqtri
      rnssopab oprex opreq1 eqid fopabco3 sylbi ) CHMAHNGOHPEGQAHCDFRZSZTALHHCG
      GAHCSAUAHMLUACTUBALUCJALHCUDUEKUFABHHCBUAZDFRULGEUMKUNDFUGCDFUGUNCDFUHJIU
      MUIUJUK $.
      $( [11-Jun-2012] $)
  $}

  ${
    $d A y z $.  $d B y $.  $d F x $.  $d G y $.  $d X x z $.  $d X y $.
    reptertr3.2 $e |- F = ( y e. X |-> ( B G y ) ) $.
    reptertr3.3 $e |- H = ( x e. X |-> A ) $.
    reptertr3.4 $e |- A e. _V $.
    $( Replacement of ` x ` by the term ` A ` in a translation. ` B ` is a
       constant.  Note that ` x ` and ` y ` can be the same. $)
    reptertr3 $p |- ( A. x e. X A e. X
      -> ( F o. H ) = ( x e. X |-> ( B G A ) ) ) $=
      ( vz wcel wral crn wss ccom co cmpt wceq cv wa copab df-mpt eqtri
      rnssopab oprex opreq2 eqid fopabco3 sylbi ) CHMAHNGOHPEGQAHDCFRZSZTALHHCG
      GAHCSAUAHMLUACTUBALUCJALHCUDUEKUFABHHCDBUAZFRULGEUMKDUNFUGDCFUGUNCDFUHJIU
      MUIUJUK $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A y z $.  $d B y $.  $d C y $.  $d F x $.  $d G y $.  $d X x z $.
    $d X y $.
    reptertr4.2 $e |- F = ( y e. X |-> ( ( B G y ) G C ) ) $.
    reptertr4.3 $e |- H = ( x e. X |-> A ) $.
    reptertr4.4 $e |- A e. _V $.
    $( Replacement of ` x ` by the term ` A ` in a translation. ` B ` and ` C `
       are constant. $)
    reptertr4 $p |- ( A. x e. X A e. X
      -> ( F o. H ) = ( x e. X |-> ( ( B G A ) G C ) ) ) $=
      ( vz wcel wral crn wss ccom co cmpt wceq cv wa copab df-mpt eqtri
      rnssopab oprex opreq2 opreq1d eqid fopabco3 sylbi ) CINAIOHPIQFHRAIDCGSZE
      GSZTZUAAMIICHHAICTAUBINMUBCUAUCAMUDKAMICUEUFLUGABIICDBUBZGSZEGSUOHFUPLURE
      GUHUNEGUHUQCUAURUNEGUQCDGUIUJKJUPUKULUM $.
      $( [2-Jul-2012] $)
  $}


  ${
    $d A x y $.  $d B x y $.  $d E x $.  $d G x y $.  $d X x y $.
    cmperltr.1 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    cmperltr.2 $e |- E = ( y e. X |-> ( A G y ) ) $.
    cmperltr.3 $e |- H = ( x e. X |-> ( x G B ) ) $.
    cmperltr.4 $e |- X = ran G $.
    $( A right and left translation expressed as a composite.  Note that ` x `
       and ` y ` can't be the same. $)
    cmperltr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X )
      -> F = ( E o. H ) ) $=
      ( cgr wcel w3a cv co cmpt ccom wceq grpoass 3exp2 com34 3imp1 mpteq2dv
      syl5eq wral wa wi grpocl 3exp com23 imp r19.21aiv 3adant2 oprex
      reptertr3 syl eqtr4d ) GNOZCIOZDIOZPZFAICAQZDGRZGRZSZEHTZVDAICVEGRDGRZSVH
      FVDAIVJVGVAVBVCVEIOZVJVGUAZVAVBVKVCVLVAVBVKVCVLCVEDGIMUBUCUDUEUFJUGVDVFIO
      ZAIUHZVIVHUAVAVCVNVBVAVCUIVMAIVAVCVKVMUJVAVKVCVMVAVKVCVMVEDGIMUKULUMUNUOU
      PABVFCEGHIKLVEDGUQURUSUT $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.  $d F x $.  $d G x y $.
    $d X x y $.
    cmprltr.1 $e |- F = ( y e. X |-> ( ( A G y ) G C ) ) $.
    cmprltr.2 $e |- E = ( x e. X |-> ( ( B G x ) G D ) ) $.
    cmprltr.3 $e |- X = ran G $.
    $( Composite of two right and left translations.  Note that ` x ` and ` y `
       can't be the same.  See ~ cmprltr2 for a more general version. $)
    cmprltr $p |- ( ( G e. GrpOp /\
      ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) -> ( F o. E ) =
         ( x e. X |-> ( ( ( A G B ) G x ) G ( D G C ) ) ) ) $=
      ( cgr wcel wa w3a ccom cv co cmpt wral wceq wi simpl1 grpocl adantr
      simpr syl111anc 3exp1 com34 3imp r19.21aiv 3exp com3r adantl com13 com12
      oprex reptertr4 syl simpl2l adantld imp 3adant3 simpl3r grpoass eqcomd
      syl13anc simpl2r opreq1d eqtrd imp32 simpl3l mpteq2dv ) INOZCJOZDJOZPZEJO
      ZFJOZPZQZHGRZAJCDASZITZFITZITZEITZUAZAJCDITZWEITZFEITITZUAWCWGJOZAJUBZWDW
      JUCVPVSWBWOVSVPWBWOUDZVRVPWPUDVQWBVPVRWOWAVPVRWOUDUDVTVPVRWAWOVPVRWAWOVPV
      RWAQWNAJVPVRWAWEJOZWNUDVPVRWQWAWNVPVRWQWAWNVPVRWQQZWAPVPWFJOZWAWNVPVRWQWA
      UEWRWSWADWEIJMUFZUGWRWAUHWFFIJMUFUIUJUKULUMUNUOUPUQUPURULABWGCEHIGJKLWFFI
      USUTVAWCAJWIWMWCWQPZWIWLFITZEITZWMXAWHXBEIXAWHCWFITZFITZXBXAVPVQWSWAWHXEU
      CVPVSWBWQUEZVQVRVPWBWQVBZWCWQWSVPVSWQWSUDZWBVPVSXHVPVRXHVQVPVRWQWSWTUNVCV
      DVEVDVTWAVPVSWQVFZVPVQWSWAQPXEWHCWFFIJMVGVHVIXAXDWLFIXAWLXDXAVPVQVRWQWLXD
      UCXFXGVQVRVPWBWQVJWCWQUHCDWEIJMVGVIVHVKVLVKXAVPWLJOZWAVTXCWMUCXFWCWQXJVPV
      SWQXJUDZWBVPVQVRXKVPVQVRWQXJVPVQVRQZWQPVPWKJOZWQXJVPVQVRWQUEXLXMWQCDIJMUF
      UGXLWQUHWKWEIJMUFUIUJVMVEVDXIVTWAVPVSWQVNWLFEIJMVGVIVLVOVL $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A a u x $.  $d A u z $.  $d B a u x $.  $d B u z $.  $d C a u x $.
    $d C u z $.  $d D a u x $.  $d D u z $.  $d F u $.  $d G a u x $.
    $d G u z $.  $d X u x $.  $d X u z $.
    cmprltr2.1 $e |- F = ( z e. X |-> ( ( A G z ) G C ) ) $.
    cmprltr2.2 $e |- E = ( x e. X |-> ( ( B G x ) G D ) ) $.
    cmprltr2.3 $e |- X = ran G $.
    $( Composite of two right and left translations.  No restriction: ` x ` and
       ` z ` can be equal. $)
    cmprltr2 $p |- ( ( G e. GrpOp /\
      ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) -> ( F o. E ) =
         ( x e. X |-> ( ( ( A G B ) G x ) G ( D G C ) ) ) ) $=
      ( vu va cgr wcel wa w3a ccom co cv cmpt ax-17 weq opreq2 opreq1d cbvmpt
      eqtri cmprltr syl6eq ) IPQCJQDJQREJQFJQRSHGTNJCDIUAZNUBZIUAZFEIUAZIUAZUCA
      JULAUBZIUAZUOIUAZUCNBCDEFGHIJKGAJDUQIUAZFIUAZUCNJDUMIUAZFIUAZUCLANOJVAVCO
      UBZVAQNUDVDVCQAUDANUEUTVBFIUQUMDIUFUGUHUIMUJNAOJUPUSVDUPQAUDVDUSQNUDNAUEU
      NURUOIUMUQULIUFUGUHUK $.
      $( [2-Jul-2012] $)
  $}


  ${
    rltr.1 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    rltr.2 $e |- X = ran G $.

    ${
      $d X x $.
      $( The domain of a right and left translation. $)
      rltrdom $p |- dom F = X $=
        ( cv co cvv wcel wral cdm wceq oprex a1i rgen cmpdom mpbi ) BAIZEJZCEJZ
        KLZAFMDNFOUDAFUDUAFLUBCEPQRAFUCDGST $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d X x $.
      $( A right and left translation is a set. $)
      rltrset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( wfun cdm cvv wcel cgr funex cv co cmpfun unsgrp cmpdom2 syl5eqel
        sylancr ) DIDJZKLDKLEMLZKDNAFBAOEPZCEPDGQUCFKUBEFHRAFUDCDEGSTUA $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d A x y $.  $d B x y $.  $d G x y $.  $d X x y $.
      $( The range of a right and left translation.  Note that ` A ` and ` B `
         are constant. $)
      rltrran $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) -> ran F = X ) $=
        ( vy cgr wcel w3a cv co wceq wrex cab crn wi eleq1 imbi2d wa simpr1
        simpr2 simpl grpocl syl111anc simpr3 3jca ex syl6 syl5cbir r19.23aiv
        com12 cgn cfv simpl1 eqid grpoinvcl 3adant3 adantr simpr 3adant2 cgi
        simpl3 grpoass syl13anc grpolinv syl11anc eqcomd opreq2d grporid
        3ad2antl1 3eqtr2rd grpolid opreq1d simpl2 grporinv 3eqtrd eqtrd opreq2
        eqeq2d rcla4ev impbid abbi1dv cmpran syl5eq ) EJKZBFKZCFKZLZIMZBAMZENZC
        ENZOZAFPZIQFDRWKWQIFWKWQWLFKZWQWKWRWPWKWRSZAFWPWSWKWOFKZSWMFKZWPWRWTWKW
        LWOFTUAXAWKWHWNFKZWJLZWTXAWKXCXAWKUBZWHXBWJXAWHWIWJUCZXDWHWIXAXBXEXAWHW
        IWJUDXAWKUEBWMEFHUFUGXAWHWIWJUHUIUJWNCEFHUFUKULUMUNWKWRWQWKWRUBZBEUOUPZ
        UPZWLCXGUPZENZENZFKZWLBXKENZCENZOZWQXFWHXHFKZXJFKZXLWHWIWJWRUQZWKXPWRWH
        WIXPWJBEXGFHXGURZUSUTVAZXFWHWRXIFKZXQXRWKWRVBZWKYAWRWHWJYAWICEXGFHXSUSV
        CVAZWLXIEFHUFUGZXHXJEFHUFUGXFWLBXHENZXJENZCENZXNXFWLXJCENZEVDUPZXJENZCE
        NYGXFYHWLXICENZENZWLYIENZWLXFWHWRYAWJYHYLOXRYBYCWHWIWJWRVEZWLXICEFHVFVG
        XFYIYKWLEXFYKYIXFWHWJYKYIOXRYNCYIEXGFHYIURZXSVHVIVJVKWHWIWRYMWLOWJWLYIE
        FHYOVLVMVNXFXJYJCEXFWHXQXJYJOXRYDWHXQUBYJXJXJYIEFHYOVOVJVIVPXFYJYFCEXFY
        IYEXJEXFYEYIXFWHWIYEYIOXRWHWIWJWRVQZBYIEXGFHYOXSVRVIVJVPVPVSXFYFXMCEXFW
        HWIXPXQYFXMOXRYPXTYDBXHXJEFHVFVGVPVTWPXOAXKFWMXKOZWOXNWLYQWNXMCEWMXKBEW
        AVPWBWCVIUJWDWEAIFWODGWFWG $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d A u x y $.  $d B u x y $.  $d G u x y $.  $d X u x y $.
      $( A right and left translation is a bijection. $)
      rltrooo $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X )
           -> F : X -1-1-onto-> X ) $=
        ( vy vu cv co cmpt wceq cgr wcel w3a wf1o wi f1oeq1 wfn crn cfv weq
        wral cvv oprex a1i rgen eqid mptfng sylib rltrran wa fvopab2b mpan2
        ad2antrl opreq2 opreq1d fvmpt ad2antll eqeq12d simpl wb simprl1 simpl1
        simpl2 simprl grpocl syl111anc simprr simpl3 3jca adantl grporcan
        syl11anc mpbid simprrl simprrr simprl2 grpolcan syl13anc ex syl6bi
        pm2.43a r19.21aivv hbmpt1 ax-17 dff1o6f sylibr syl5bir ax-mp ) DAFBAKZE
        LZCELZMZNZEOPZBFPZCFPZQZFFDRZSGWQXBFFWPRZXAFFDWPTXAWPFUAZWPUBFNZWMWPUCZ
        IKZWPUCZNZAIUDZSZIFUEAFUEZQXCXAXDXEXLXAWOUFPZAFUEZXDXNXAXMAFXMWMFPZWNCE
        UGZUHUIUHAFWOWPWPUJZUKULABCWPEFXQHUMXAXKAIFFXAXOXGFPZUNZXKXIXAXSUNZXJXT
        XIWOBXGELZCELZNZXTXJSXTXFWOXHYBXOXFWONZXAXRXOXMYDXPAFWOUFWPXQUOUPUQXRXH
        YBNXAXOAXGWOYBFWPXJWNYACEWMXGBEURUSXQYACEUGUTVAVBYCXTXJYCXTUNZWNYANZXJY
        EYCYFYCXTVCYEWRWNFPZYAFPZWTQZYCYFVDWRWSWTXSYCVEZXTYIYCXTYGYHWTXTWRWSXOY
        GWRWSWTXSVFZWRWSWTXSVGZXAXOXRVHBWMEFHVIVJXTWRWSXRYHYKYLXAXOXRVKBXGEFHVI
        VJWRWSWTXSVLVMVNWNYACEFHVOVPVQYEWRXOXRWSYFXJVDYJYCXAXOXRVRYCXAXOXRVSWRW
        SWTXSYCVTWMXGBEFHWAWBVQWCWDWEWCWFVMAIJFFWPAJFWOWGJKWPPIWHWIWJWKWL $.
        $( [2-Jul-2012] $)
    $}
  $}

  $( Compare fnopab2g and fnopab2 . If an antecedent, the condition B e. _V
     must be quantified. If a premisse it needn't. $)

