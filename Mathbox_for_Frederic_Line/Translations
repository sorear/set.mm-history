$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Translations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    trfun.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trinv.1 $e |- X = ran G $.
    ${
      $d A x $.  $d G x $.  $d X x $.
      $( The domain of a right translation.  The term ` A ` is a constant:
         ` x ` is not present.  (Contributed by FL, 21-Jun-2010.) $)
      trdom2 $p |- ( ( G e. GrpOp /\ A e. X ) -> dom F = X ) $=
        ( cgr wcel wa cv co cvv wral cdm wceq oprex a1i r19.21aiva cmpdom sylib
        ) DHIBEIJZAKZBDLZMIZAENCOEPUBUEAEUEUBUCEIJUCBDQRSAEUDCFTUA $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d X x $.
      $( A right translation is a set.  (Contributed by FL, 19-Sep-2010.) $)
      trset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( cgr wcel wfun cdm cvv cv cmpfun cmpdom2 unsgrp syl5eqel funex sylancr
        co ) DHIZCJCKZLICLIAEAMZBDTCFNUAUBELAEUCBCDFODEGPQLCRS $.
        $( [19-Sep-2010] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( The range of a right translation.  The term ` A ` is a constant: ` x `
         is not present.  (Contributed by FL, 21-Jun-2010.) $)
      trran2 $p |- ( ( G e. GrpOp /\ A e. X ) -> ran F = X ) $=
        ( vy cgr wcel wa cv co wceq wi grpocl cfv eqid adantr eqcomd crn cmpran
        wrex cab 3exp com23 imp impcom eleq1 syl5ibrcom expcom r19.23adv simpll
        cgn grpoinvcl syl111anc grpolinv opreq2d grporid adantlr simplr grpoass
        simpr syl13anc 3eqtr4d opreq1 eqeq2d rcla4ev syl11anc ex impbid abbi1dv
        cgi syl5eq ) DIJZBEJZKZCUAHLZALZBDMZNZAEUCZHUDEAHEVTCFUBVQWBHEVQWBVREJZ
        VQWAWCAEVSEJZVQWAWCOWDVQKWCWAVTEJZVQWDWEVOVPWDWEOVOWDVPWEVOWDVPWEVSBDEG
        PUEUFUGUHVRVTEUIUJUKULVQWCWBVQWCKZVRBDUNQZQZDMZEJZVRWIBDMZNZWBWFVOWCWHE
        JZWJVOVPWCUMZVQWCVCZVQWMWCBDWGEGWGRZUOSZVRWHDEGPUPWFVRDVMQZDMZVRWHBDMZD
        MZVRWKWFWRWTVRDWFWTWRVQWTWRNWCBWRDWGEGWRRZWPUQSTURVOWCVRWSNVPVOWCKWSVRV
        RWRDEGXBUSTUTWFVOWCWMVPWKXANWNWOWQVOVPWCVAVRWHBDEGVBVDVEWAWLAWIEVSWINVT
        WKVRVSWIBDVFVGVHVIVJVKVLVN $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.  $d G x y z $.  $d X x y z $.
      $( A right translation is a bijection.  The term ` A ` is a constant.
         (Contributed by FL, 21-Jun-2010.) $)
      trooo $p |- ( ( G e. GrpOp /\ A e. X ) -> F : X -1-1-onto-> X ) $=
        ( vy vz wcel wa wfn crn wceq cv cfv wi wral cvv wb cgr w3a co oprex a1i
        wf1o rgen fopab2ga mpbii trran2 fvmpt2 mpan2 simpr adantl opreq1 fvmptg
        ad2antrl jctir syl eqeq12d biimpd grporcan 3exp2 imp3a com23 r19.21aivv
        imp31 sylibd ex 3jca cmpt hbmpt1 ax-17 f1oeq1 ax-mp fneq1i rneqi eqeq1i
        dff1o6f fveq1i eqeq12i imbi1i 2ralbii 3anbi123i 3bitr4i sylibr ) DUAJZB
        EJZKZCELZCMZENZAOZCPZHOZCPZNZWMWONZQZHERAERZUBZEECUFZWIWJWLWTWIWMBDUCZS
        JZAERZWJXDAEXDWMEJZWMBDUDZUEUGXEWJTWIAEXCCFUHUEUIABCDEFGUJWIWSAHEEWIXFW
        OEJZKZWSWIXIKZWQXCWOBDUCZNZWRXJWQXLXJWNXCWPXKXFWNXCNZWIXHXFXDXMXGAEXCSC
        FUKULUQXJXHXKSJZKZWPXKNXIXOWIXIXHXNXFXHUMWOBDUDURUNAWOXCXKESCWMWOBDUOFU
        PUSUTVAWGWHXIXLWRTZWGXIWHXPWGXFXHWHXPQWGXFXHWHXPWMWOBDEGVBVCVDVEVGVHVIV
        FVJEEAEXCVKZUFZXQELZXQMZENZWMXQPZWOXQPZNZWRQZHERAERZUBXBXAAHIEEXQAIEXCV
        LIOXQJHVMVSCXQNXBXRTFEECXQVNVOWJXSWLYAWTYFECXQFVPWKXTECXQFVQVRWSYEAHEEW
        QYDWRWNYBWPYCWMCXQFVTWOCXQFVTWAWBWCWDWEWF $.
        $( [31-Dec-2010] $) $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.  $d D y z $.  $d G x y z $.  $d X x y z $.
      trinv.2 $e |- D = ( /g ` G ) $.
      $( The converse of a right translation.  The term ` A ` is a constant.
         (Contributed by FL, 21-Jun-2010.) $)
      trinv $p |- ( ( G e. GrpOp /\ A e. X )
          -> `' F = ( x e. X |-> ( x D A ) ) ) $=
        ( vy vz wcel wa co wceq cfv wral cvv wfn wb cgr ccnv cv cmpt wf1o trooo
        adantr grpdivcl 3exp com23 imp31 jca oprex opreq1 cmpbvb fvmptg sylancl
        grpnpcan eqtrd f1ocnvfv sylc simpr eqid fvmpt2 eqtr4d r19.21aiva dff1o4
        simprbi syl a1i rgen fopab2ga mpbii hbmpt1 hbcnv eqfnfv2f cnveqi fneq1i
        anbi1i eqeq1i fveq1i ralbii bibi12i 3imtr4i syl11anc mpbird ) EUALZBFLZ
        MZDUBZAFAUCZBCNZUDZOZWKWJPZWKWMPZOZAFQZWIWQAFWIWKFLZMZWOWLWPWTFFDUEZWLF
        LZMWLDPZWKOWOWLOWTXAXBWIXAWSABDEFGHUFZUGWGWHWSXBWGWSWHXBWGWSWHXBWKBCEFH
        IUHUIUJUKZULWTXCWLBENZWKWTXBXFRLXCXFOXEWLBEUMJWLJUCZBENZXFFRDXGWLBEUNAJ
        FWKBENZXHDGWKXGBEUNUOUPUQWGWHWSXFWKOZWGWSWHXJWGWSWHXJWKBCEFHIURUIUJUKUS
        FFWLWKDUTVAWTWSWLRLZWPWLOWIWSVBWKBCUMZAFWLRWMWMVCZVDUQVEVFWIWJFSZWMFSZW
        NWRTZWIXAXNXDXADFSXNFFDVGVHVIWIXKAFQZXOXKAFXKWSXLVJVKXQXOTWIAFWLWMXMVLV
        JVMAFXIUDZUBZFSZXOMXSWMOZWKXSPZWPOZAFQZTXNXOMXPAKFXSWMAKXRAKFXIVNVOAKFW
        LVNVPXNXTXOFWJXSDXRGVQZVRVSWNYAWRYDWJXSWMYEVTWQYCAFWOYBWPWKWJXSYEWAVTWB
        WCWDWEWF $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A a b x y $.  $d B a b x y $.  $d F x $.  $d G a b x y $.
      $d X a b x y $.
      cmprtr.1 $e |- H = ( x e. X |-> ( x G B ) ) $.
      $( Composite of two right translations.  The terms ` A ` and ` B ` are
         constant.  Don't use.  See ~ cmprtr2 .  (Contributed by FL,
         17-Oct-2010.) $)
      cmprtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( x G ( B G A ) ) ) ) $=
        ( vy va vb wcel cv co wceq wa copab ax-17 cgr w3a crn wss trran2 eqimss
        ccom cmpt syl 3adant2 oprex opreq1 df-mpt eqtri wb eleq1 adantr eqeq12d
        simpr anbi12d cbvopab 3eqtri eqid fopabco simpl1 simpl3 simpl2 syl13anc
        weq grpoass eqeq2d pm5.32da opabbid eqcomi a1i 3eqtrd ) EUANZBGNZCGNZUB
        ZDFUGZAOZGNZKOZWBCEPZBEPZQZRZAKSZWCWDWBCBEPEPZQZRZAKSZAGWJUHZVTFUCZGUDZ
        WAWIQVQVSWPVRVQVSRWOGQWPACFEGJIUEWOGUFUIUJAKLMKGGWELOZBEPZWFFDWIWBCEUKW
        QBEUKWEBEUKWQWEBEULFAGWEUHWCWDWEQRAKSJAKGWEUMUNDAGWBBEPZUHWCWDWSQZRZAKS
        WQGNZMOZWRQZRZLMSHAKGWSUMXAXEAKLMXALTXAMTXEATXEKTALVIZKMVIZRZWCXBWTXDXF
        WCXBUOXGWBWQGUPUQXHWDXCWSWRXFXGUSXFWSWRQXGWBWQBEULUQURUTVAVBWIVCVDUIVTW
        HWLAKVTATVTKTVTWCWGWKVTWCRZWFWJWDXIVQWCVSVRWFWJQVQVRVSWCVEVTWCUSVQVRVSW
        CVFVQVRVSWCVGWBCBEGIVJVHVKVLVMWMWNQVTWNWMAKGWJUMVNVOVP $.
        $( [17-Oct-2010] $)
    $}

    ${
      $d A x y $.  $d B x y $.  $d F y $.  $d G x y $.  $d X x y $.
      cmprtr2.1 $e |- H = ( x e. X |-> ( x G B ) ) $.
      $( Composite of two right translations.  ( ~ cmprtr with a distinct
         variable condition relaxed.)  (Contributed by FL, 1-Jan-2011.) $)
      cmprtr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( x G ( B G A ) ) ) ) $=
        ( vy cgr wcel cv co cmpt eqid opreq1 cmpbvb eqtri ccom cmprtr syl6eq
        w3a ) ELMBGMCGMUDDFUAKGKNZCBEOZEOZPZAGANZUFEOZPKBCDEFGDAGUIBEOZPZKGUEBE
        OZPHAKGUKUMULULQUIUEBERSTIFAGUICEOZPZKGUECEOZPJAKGUNUPUOUOQUIUECERSTUBK
        AGUGUJUHUHQUEUIUFERSUC $.
        $( [1-Jan-2011] $)
    $}

    ${
      $d A x $.  $d B a u $.  $d F a u $.  $d G x $.  $d X x $.
      $( The image of a set through a translation.  (Contributed by FL,
         30-Dec-2010.) $)
      imtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X ) ->
       ( F " B ) = { a | E. u e. B a = ( F ` u ) } ) $=
        ( cgr wcel cpw w3a cima cv wceq wrex cab wi cfv cdm co cmpfun wa trdom2
        wfun wss pweq eleq2d biimpd eqcoms elpwi syl6 syl dfimafn sylancr eqcom
        3impia rexbii abbii syl6eq ) FKLZCGLZDGMZLZNZEDOZBPEUAZHPZQZBDRZHSZVJVI
        QZBDRZHSVGEUGDEUBZUHZVHVMQAGAPCFUCEIUDVCVDVFVQVCVDUEVPGQZVFVQTACEFGIJUF
        VRVFDVPMZLZVQVFVTTGVPGVPQZVFVTWAVEVSDGVPUIUJUKULDVPUMUNUOUSBHDEUPUQVLVO
        HVKVNBDVIVJURUTVAVB $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A a v x $.  $d B a x $.  $d F a x $.  $d G a v x $.  $d X a x $.
      prsubrtr.1 $e |- H = ( cset ` G ) $.
      $( The product of a subset ` B ` of ` X ` by an element of ` X ` is the
         image of ` B ` by a right translation.  (Contributed by FL,
         30-Dec-2010.) $)
      prsubrtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X )
        -> ( B H { A } ) = ( F " B ) ) $=
        ( va vv cgr wcel cpw cv co wceq wrex wb w3a csn cab cima wa elsn opreq2
        cfv eqeq2d sylbi rexbiia c0 wne snnzg 3ad2ant2 adantr bicomd syl syl5bb
        r19.9rzv cvv wi elelpwi expcom 3ad2ant3 imp oprex fvmpt2 sylancl eqcomd
        bitrd rexbidva abbidv cdm crn grprndm syl5eq pweq eleq2d biimpa 3adant2
        simp1 snelpwg adantl mpbid 3adant3 eqid iscst2 syl111anc imtr 3eqtr4d )
        EMNZBGNZCGOZNZUAZKPZAPZLPZEQZRZLBUBZSZACSZKUCZWQWRDUHZRZACSZKUCCXBFQZDC
        UDWPXDXHKWPXCXGACWPWRCNZUEZXCWQWRBEQZRZXGXCXMLXBSZXKXMXAXMLXBWSXBNWSBRZ
        XAXMTLBUFXOWTXLWQWSBWREUGUIUJUKXKXBULUMZXNXMTWPXPXJWMWLXPWOBGUNUOUPXPXM
        XNXMLXBUTUQURUSXKXLXFWQXKXFXLXKWRGNZXLVANXFXLRWPXJXQWOWLXJXQVBWMXJWOXQW
        RCGVCVDVEVFWRBEVGAGXLVADHVHVIVJUIVKVLVMWPWLCEVNVNZOZNZXBXSNZXIXERWLWMWO
        WBWLWOXTWMWLWOXTWLWNXSCWLGXRRWNXSRWLGEVOXRIEVPVQZGXRVRURVSVTWAWLWMYAWOW
        LWMUEBXRNZYAWLWMYCWLGXRBYBVSVTWMYCYATWLBXRGWCWDWEWFLACXBEFMXRKXRWGJWHWI
        AABCDEGKHIWJWK $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A a x $.  $d B a $.  $d F a $.  $d G a x $.  $d X a x $.
      prsubrtr2.1 $e |- H = ( cset ` G ) $.
      $( The product of a subset ` B ` of ` X ` by an element of ` X ` is the
         image of ` B ` by a right translation.  ( ~ prsubrtr with a distinct
         variable condition relaxed.)  (Contributed by FL, 1-Jan-2011.) $)
      prsubrtr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X )
        -> ( B H { A } ) = ( F " B ) ) $=
        ( va cv co cmpt eqid opreq1 cmpbvb eqtri prsubrtr ) KBCDEFGDAGALZBEMZNZ
        KGKLZBEMZNHAKGUAUDUBUBOTUCBEPQRIJS $.
        $( [1-Jan-2011] $)
    $}

    ${
      $d A x $.  $d G x $.  $d X x $.
      caytr.1 $e |- H = ( cset ` G ) $.
      $( "It follows that if the entire group is multiplied by any one of the
         symbols, either as further or nearer factor, the effect is simply to
         reproduce the group... ."  Cayley, On the theory of groups, as
         depending on the symbolic equation th^n = 1, 1854.  (it is the
         original paper where the axiomatic definition of a group was given for
         the first time.)  (Contributed by FL, 15-Oct-2012.) $)
      caytr $p |- ( ( G e. GrpOp /\ A e. X ) -> ( X H { A } ) = X ) $=
        ( cgr wcel wa csn co cima crn cpw wceq cvv syl wss ssid wb unsgrp elpwg
        adantr mpbiri prsubrtr2 mpd3an3 wf1o trooo f1ofn fnima trran2 3eqtrd
        wfn ) DJKZBFKZLZFBMENZCFOZCPZFUQURFFQKZUTVARUSVCFFUAZFUBUSFSKZVCVDUCUQV
        EURDFHUDUFFFSUETUGABFCDEFGHIUHUIUSCFUPZVAVBRUSFFCUJVFABCDFGHUKFFCULTFCU
        MTABCDFGHUNUO $.
        $( [15-Oct-2012] $)
    $}

  $}


  ${
    ltrdom.1 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrdom.2 $e |- X = ran G $.
    ${
      $d A x $.  $d G x $.  $d X x $.
      $( The domain of a left translation.  The term ` A ` is a constant.
         (Contributed by FL, 26-Apr-2012.) $)
      ltrdom $p |- ( ( G e. GrpOp /\ A e. X ) -> dom F = X ) $=
        ( cgr wcel wa cv co cvv wral cdm wceq oprex a1i r19.21aiva cmpdom sylib
        ) DHIBEIJZBAKZDLZMIZAENCOEPUBUEAEUEUBUCEIJBUCDQRSAEUDCFTUA $.
        $( [26-Apr-2012] $)
    $}

    ${
      $d X x $.
      $( A left translation is a set.  (Contributed by FL, 28-Apr-2012.) $)
      ltrset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( cgr wcel wfun cdm cvv cv cmpfun cmpdom2 unsgrp syl5eqel funex sylancr
        co ) DHIZCJCKZLICLIAEBAMZDTCFNUAUBELAEBUCCDFODEGPQLCRS $.
        $( [28-Apr-2012] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( The range of a left translation.  The term ` A ` is a constant.
         (Contributed by FL, 28-Apr-2012.) $)
      ltrran2 $p |- ( ( G e. GrpOp /\ A e. X ) -> ran F = X ) $=
        ( vy cgr wcel wa crn cv co wceq grpocl cfv eqid adantr eqcomd cmpran wi
        wrex cab 3expia impcom syl5ibrcom expcom r19.23adv cgn simpll grpoinvcl
        eleq1 simpr syl111anc grpolid adantlr grporinv opreq1d grpoass syl13anc
        cgi simplr 3eqtrd opreq2 eqeq2d rcla4ev syl11anc impbid abbi1dv syl5eq
        ex ) DIJZBEJZKZCLHMZBAMZDNZOZAEUCZHUDEAHEVRCFUAVOVTHEVOVTVPEJZVOVSWAAEV
        QEJZVOVSWAUBWBVOKWAVSVREJZVOWBWCVMVNWBWCBVQDEGPUEUFVPVREUMUGUHUIVOWAVTV
        OWAKZBDUJQZQZVPDNZEJZVPBWGDNZOZVTWDVMWFEJZWAWHVMVNWAUKZVOWKWABDWEEGWERZ
        ULSZVOWAUNZWFVPDEGPUOWDVPDVBQZVPDNZBWFDNZVPDNZWIVMWAVPWQOVNVMWAKWQVPVPW
        PDEGWPRZUPTUQWDWPWRVPDVOWPWROWAVOWRWPBWPDWEEGWTWMURTSUSWDVMVNWKWAWSWIOW
        LVMVNWAVCWNWOBWFVPDEGUTVAVDVSWJAWGEVQWGOVRWIVPVQWGBDVEVFVGVHVLVIVJVK $.
        $( [28-Apr-2012] $)
    $}

    ${
      $d A a b x z $.  $d G a b x z $.  $d X a b x z $.
      $( A left translation is a bijection.  The term ` A ` is a constant.
         (Contributed by FL, 29-Apr-2012.) $)
      ltrooo $p |- ( ( G e. GrpOp /\ A e. X ) -> F : X -1-1-onto-> X ) $=
        ( va vb vz cv co wceq wcel wa wi wral w3a 3exp 3adant3 cmpt cgr wfn crn
        wf1o cfv weq cvv oprex a1i rgen eqid mptfng sylib ltrran2 simprl grpocl
        com3r adantr com3l imp31 opreq2 fvmptg syl11anc eqcomd simp3 simprr imp
        3expia adantld jca syl 3eqtrd wb grpolcan expcom com34 mpbid r19.21aivv
        3jca ax-17 dff1o6f sylibr f1oeq1 syl5ibr ax-mp ) CAEBAKZDLZUAZMZDUBNZBE
        NZOZEECUEZPFWMWNWJEEWIUEZWMWIEUCZWIUDEMZHKZWIUFZIKZWIUFZMZHIUGZPZIEQHEQ
        ZRWOWMWPWQXEWMWHUHNZAEQZWPXGWMXFAEXFWGENBWGDUIUJUKUJAEWHWIWIULZUMUNABWI
        DEXHGUOWMXDHIEEWMWRENZWTENZOZXBXCWMXKXBRZBWRDLZBWTDLZMZXCXLXMWSXAXNWMXK
        XMWSMXBWMXKOZWSXMXPXIXMENZWSXMMWMXIXJUPWKWLXKXQXKWKWLXQXIWKWLXQPPXJWKWL
        XIXQWKWLXIXQBWRDEGUQSURUSUTVAAWRWHXMEEWIWGWRBDVBXHVCVDVETWMXKXBVFXLXJXN
        ENZOZXAXNMWMXKXSXBXPXJXRWMXIXJVGWMXKXRWMXJXRXIWKWLXJXRBWTDEGUQVIVJVHVKT
        AWTWHXNEEWIWGWTBDVBXHVCVLVMWMXKXOXCVNZXBWKWLXKXTXKWKWLXTXIXJWKWLXTPPXIX
        JWLWKXTXIXJWLWKXTPWKXIXJWLRXTWRWTBDEGVOVPSVQVHUTVATVRSVSVTHIJEEWIJKWINZ
        HWAYAIWAWBWCEECWIWDWEWF $.
        $( [29-Apr-2012] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( Left translation expressed as a composite.  (Contributed by FL,
         3-Jul-2012.) $)
      ltrcmp $p |- ( ( G e. GrpOp /\ A e. X ) ->
        F = ( G o. { <. x , y >. | ( x e. X /\ y = <. A , x >. ) } ) ) $=
        ( cgr wcel wa cv cop cxp wral wfn wceq copab ccom opelxpi adantll grpfo
        r19.21aiva wfo fofn syl adantr eqid co cmpt df-mpt df-opr eqeq2i anbi2i
        cfv opabbii 3eqtri fnopabco2b syl11anc ) EIJZCFJZKZCALZMZFFNZJZAFOEVEPZ
        DEVCFJZBLZVDQKABRZSQVBVFAFVAVHVFUTCVCFFTUAUCUTVGVAUTVEFEUDVGEFHUBVEFEUE
        UFUGABFVDVEVJDEVJUHDAFCVCEUIZUJVHVIVKQZKZABRVHVIVDEUOZQZKZABRGABFVKUKVM
        VPABVLVOVHVKVNVICVCEULUMUNUPUQURUS $.
        $( [3-Jul-2012] $)
    $}

    ${
      $d A x y z $.  $d F z $.  $d G x y z $.  $d N x y z $.  $d X x y z $.
      ltrinvlem.2 $e |- D = ( /g ` G ) $.
      ltrinvlem.3 $e |- N = ( inv ` G ) $.
      $( The converse of a left translation.  The term ` A ` is a constant.
         (Contributed by FL, 30-Apr-2012.) $)
      ltrinvlem $p |- ( ( G e. GrpOp /\ A e. X )
          -> `' F = ( x e. X |-> ( ( N ` A ) G x ) ) ) $=
        ( vy vz wcel wa cfv cv co wceq wf1o ccnv cmpt wral ltrooo adantr simpll
        cgr grpoinvcl grpocl syl111anc jca simplr opreq2 cmpbvb fvmptg syl11anc
        simpr w3a grpoass eqcomd syl13anc eqid grporinv opreq1d grpolid adantlr
        cgi eqtrd 3eqtrd f1ocnvfv sylc fvmpt2 eqtr4d r19.21aiva wb f1ocnv f1ofn
        wfn 3syl syldan syl wal wi hbmpt1 hbcnv cnveq eleq2d hbth albid 3imtr4d
        a1i ax-mp eqfnfv2f mpbird ) EUGNZBGNZOZDUAZAGBFPZAQZERZUBZSZWTWRPZWTXBP
        ZSZAGUCZWQXFAGWQWTGNZOZXDXAXEXIGGDTZXAGNZOXADPZWTSXDXASXIXJXKWQXJXHABDE
        GHIUDZUEXIWOWSGNZXHXKWOWPXHUFZWQXNXHBEFGIKUHZUEZWQXHUQZWSWTEGIUIUJZUKXI
        XLBXAERZBWSERZWTERZWTXIXKXTGNZXLXTSXSXIWOWPXKYCXOWOWPXHULZXSBXAEGIUIUJL
        XABLQZERZXTGGDYEXABEUMALGBWTERZYFDHWTYEBEUMUNUOUPXIWOWPXNXHXTYBSXOYDXQX
        RWOWPXNXHUROYBXTBWSWTEGIUSUTVAXIYBEVGPZWTERZWTXIYAYHWTEWQYAYHSXHBYHEFGI
        YHVBZKVCUEVDWOXHYIWTSWPWTYHEGIYJVEVFVHVIGGXAWTDVJVKXIXHXKXEXASXRXSAGXAG
        XBXBVBZVLUPVMVNWQWRGVRZXBGVRZXCXGVOWQXJGGWRTYLXMGGDVPGGWRVQVSWQGGXBTZYM
        WOWPXNYNXPAWSXBEGYKIUDVTGGXBVQWAAMGWRXBDAGYGUBZSZMQZWRNZYRAWBZWCHYPYQYO
        UAZNZUUAAWBZYRYSUUAUUBWCYPAMYOAMGYGWDWEWKYPWRYTYQDYOWFWGZYPYRUUAAYPAHWH
        UUCWIWJWLAMGXAWDWMUPWN $.
        $( [30-Apr-2012] $)
    $}

    ${
      $d A v x y z $.  $d B v w x y z $.  $d F z $.  $d G v w x y z $.
      $d X v w x y z $.
      cmpltr.2 $e |- H = ( x e. X |-> ( B G x ) ) $.
      $( Composite of two left translations.  The terms ` A ` and ` B ` are
         constant.  Don't use.  See ~ cmpltr2 .  (Contributed by FL,
         2-Jul-2012.) $)
      cmpltr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( ( A G B ) G x ) ) ) $=
        ( vv vz vw vy wcel cv co wceq wa copab cgr w3a ccom cmpt crn wss eqimss
        ltrran2 syl 3adant2 oprex opreq2 df-mpt ax-17 weq eleq1 eqeq2d cbvopab1
        anbi12d 3eqtri eqtri eqid fopabco opreq2d syl6eq wb grpoass eqcomd 3imp
        wi 3exp2 pm5.32d opabbidv eqcomi a1i 3eqtrd ) EUAOZBGOZCGOZUBZDFUCZAPZG
        OZKPZBCWBEQZEQZRZSZAKTZWCWDBCEQWBEQZRZSZAKTZAGWJUDZVTFUEZGUFZWAWIRVQVSW
        PVRVQVSSWOGRWPACFEGJIUHWOGUGUIUJWPWALPZGOZWDBCWQEQZEQZRZSZLKTZWILMANKGG
        WSBWBEQZWTFDXCCWQEUKBWBEUKBWSEUKWBWSBEULFAGWEUDWCMPZWERZSZAMTWRXEWSRZSZ
        LMTJAMGWEUMXGXIAMLXGLUNXIAUNALUOZWCWRXFXHWBWQGUPXJWEWSXEWBWQCEULUQUSURU
        TDAGXDUDWCNPXDRSANTHANGXDUMVAXCVBVCXBWHLKAXBAUNWHLUNLAUOZWRWCXAWGWQWBGU
        PXKWTWFWDXKWSWEBEWQWBCEULVDUQUSURVEUIVTWHWLAKVTWCWGWKVQVRVSWCWGWKVFZVJV
        QVRVSWCXLVQVRVSWCUBSZWFWJWDXMWJWFBCWBEGIVGVHUQVKVIVLVMWMWNRVTWNWMAKGWJU
        MVNVOVP $.
        $( [2-Jul-2012] $)

    $}

    ${
      $d A x y $.  $d B x y $.  $d G x y $.  $d X x y $.
      cmpltr2.2 $e |- H = ( x e. X |-> ( B G x ) ) $.
      $( Composite of two left translations.  The terms ` A ` and ` B ` are
         constant.  (Contributed by FL, 2-Jul-2012.) $)
      cmpltr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( ( A G B ) G x ) ) ) $=
        ( vy cgr wcel co cv cmpt eqid opreq2 cmpbvb eqtri ccom cmpltr syl6eq
        w3a ) ELMBGMCGMUDDFUAKGBCENZKOZENZPZAGUEAOZENZPKBCDEFGDAGBUIENZPZKGBUFE
        NZPHAKGUKUMULULQUIUFBERSTIFAGCUIENZPZKGCUFENZPJAKGUNUPUOUOQUIUFCERSTUBK
        AGUGUJUHUHQUFUIUEERSUC $.
        $( [2-Jul-2012] $)
    $}
  $}

  ${
    $d A y z $.  $d B y $.  $d F x $.  $d G y $.  $d X x z $.  $d X y $.
    reptertr2.2 $e |- F = ( y e. X |-> ( y G B ) ) $.
    reptertr2.3 $e |- H = ( x e. X |-> A ) $.
    reptertr2.4 $e |- A e. _V $.
    $( Replacement of ` x ` by the term ` A ` in a translation. ` B ` is a
       constant.  Note that ` x ` and ` y ` can be the same.  (Contributed by
       FL, 11-Jun-2012.) $)
    reptertr2 $p |- ( A. x e. X A e. X
      -> ( F o. H ) = ( x e. X |-> ( A G B ) ) ) $=
      ( vz wcel wral crn co cmpt wceq cv oprex wss ccom wa copab eqtri rnssopab
      df-mpt opreq1 eqid fopabco3 sylbi ) CHMAHNGOHUAEGUBAHCDFPZQZRALHHCGGAHCQA
      SHMLSCRUCALUDJALHCUGUEKUFABHHCBSZDFPULGEUMKUNDFTCDFTUNCDFUHJIUMUIUJUK $.
      $( [11-Jun-2012] $)
  $}

  ${
    $d A y z $.  $d B y $.  $d F x $.  $d G y $.  $d X x z $.  $d X y $.
    reptertr3.2 $e |- F = ( y e. X |-> ( B G y ) ) $.
    reptertr3.3 $e |- H = ( x e. X |-> A ) $.
    reptertr3.4 $e |- A e. _V $.
    $( Replacement of ` x ` by the term ` A ` in a translation. ` B ` is a
       constant.  Note that ` x ` and ` y ` can be the same.  (Contributed by
       FL, 2-Jul-2012.) $)
    reptertr3 $p |- ( A. x e. X A e. X
      -> ( F o. H ) = ( x e. X |-> ( B G A ) ) ) $=
      ( vz wcel wral crn co cmpt wceq cv oprex wss ccom wa copab eqtri rnssopab
      df-mpt opreq2 eqid fopabco3 sylbi ) CHMAHNGOHUAEGUBAHDCFPZQZRALHHCGGAHCQA
      SHMLSCRUCALUDJALHCUGUEKUFABHHCDBSZFPULGEUMKDUNFTDCFTUNCDFUHJIUMUIUJUK $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A y z $.  $d B y $.  $d C y $.  $d F x $.  $d G y $.  $d X x z $.
    $d X y $.
    reptertr4.2 $e |- F = ( y e. X |-> ( ( B G y ) G C ) ) $.
    reptertr4.3 $e |- H = ( x e. X |-> A ) $.
    reptertr4.4 $e |- A e. _V $.
    $( Replacement of ` x ` by the term ` A ` in a translation. ` B ` and ` C `
       are constant.  (Contributed by FL, 2-Jul-2012.) $)
    reptertr4 $p |- ( A. x e. X A e. X
      -> ( F o. H ) = ( x e. X |-> ( ( B G A ) G C ) ) ) $=
      ( vz wcel wral co cmpt wceq cv oprex crn wss ccom wa copab eqtri rnssopab
      df-mpt opreq2 opreq1d eqid fopabco3 sylbi ) CINAIOHUAIUBFHUCAIDCGPZEGPZQZ
      RAMIICHHAICQASINMSCRUDAMUEKAMICUHUFLUGABIICDBSZGPZEGPUOHFUPLUREGTUNEGTUQC
      RURUNEGUQCDGUIUJKJUPUKULUM $.
      $( [2-Jul-2012] $)
  $}


  ${
    $d A x y $.  $d B x y $.  $d E x $.  $d G x y $.  $d X x y $.
    cmperltr.1 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    cmperltr.2 $e |- E = ( y e. X |-> ( A G y ) ) $.
    cmperltr.3 $e |- H = ( x e. X |-> ( x G B ) ) $.
    cmperltr.4 $e |- X = ran G $.
    $( A right and left translation expressed as a composite.  Note that ` x `
       and ` y ` can't be the same.  (Contributed by FL, 2-Jul-2012.) $)
    cmperltr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X )
      -> F = ( E o. H ) ) $=
      ( cgr wcel w3a cv co cmpt wceq grpoass 3exp2 com34 3imp1 mpteq2dva syl5eq
      ccom wral wa wi grpocl com23 imp r19.21aiv 3adant2 oprex reptertr3 eqtr4d
      3exp syl ) GNOZCIOZDIOZPZFAICAQZDGRZGRZSZEHUGZVDFAICVEGRDGRZSVHJVDAIVJVGV
      AVBVCVEIOZVJVGTZVAVBVKVCVLVAVBVKVCVLCVEDGIMUAUBUCUDUEUFVDVFIOZAIUHZVIVHTV
      AVCVNVBVAVCUIVMAIVAVCVKVMUJVAVKVCVMVAVKVCVMVEDGIMUKUSULUMUNUOABVFCEGHIKLV
      EDGUPUQUTUR $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.  $d F x $.  $d G x y $.
    $d X x y $.
    cmprltr.1 $e |- F = ( y e. X |-> ( ( A G y ) G C ) ) $.
    cmprltr.2 $e |- E = ( x e. X |-> ( ( B G x ) G D ) ) $.
    cmprltr.3 $e |- X = ran G $.
    $( Composite of two right and left translations.  Note that ` x ` and ` y `
       can't be the same.  See ~ cmprltr2 for a more general version.
       (Contributed by FL, 2-Jul-2012.) $)
    cmprltr $p |- ( ( G e. GrpOp /\
      ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) -> ( F o. E ) =
         ( x e. X |-> ( ( ( A G B ) G x ) G ( D G C ) ) ) ) $=
      ( wcel wa w3a co wceq wi grpocl cgr ccom cv cmpt wral simpl1 adantr simpr
      syl111anc 3exp1 com34 3imp r19.21aiv 3exp com3r com13 com12 reptertr4 syl
      adantl oprex simpl2l adantld imp 3adant3 simpl3r grpoass syl13anc simpl2r
      eqcomd opreq1d eqtrd imp32 simpl3l mpteq2dva ) IUANZCJNZDJNZOZEJNZFJNZOZP
      ZHGUBZAJCDAUCZIQZFIQZIQZEIQZUDZAJCDIQZWEIQZFEIQIQZUDWCWGJNZAJUEZWDWJRVPVS
      WBWOVSVPWBWOSZVRVPWPSVQWBVPVRWOWAVPVRWOSSVTVPVRWAWOVPVRWAWOVPVRWAPWNAJVPV
      RWAWEJNZWNSVPVRWQWAWNVPVRWQWAWNVPVRWQPZWAOVPWFJNZWAWNVPVRWQWAUFWRWSWADWEI
      JMTZUGWRWAUHWFFIJMTUIUJUKULUMUNUOUTUPUTUQULABWGCEHIGJKLWFFIVAURUSWCAJWIWM
      WCWQOZWIWLFIQZEIQZWMXAWHXBEIXAWHCWFIQZFIQZXBXAVPVQWSWAWHXERVPVSWBWQUFZVQV
      RVPWBWQVBZWCWQWSVPVSWQWSSZWBVPVSXHVPVRXHVQVPVRWQWSWTUNVCVDVEVDVTWAVPVSWQV
      FZVPVQWSWAPOXEWHCWFFIJMVGVJVHXAXDWLFIXAWLXDXAVPVQVRWQWLXDRXFXGVQVRVPWBWQV
      IWCWQUHCDWEIJMVGVHVJVKVLVKXAVPWLJNZWAVTXCWMRXFWCWQXJVPVSWQXJSZWBVPVQVRXKV
      PVQVRWQXJVPVQVRPZWQOVPWKJNZWQXJVPVQVRWQUFXLXMWQCDIJMTUGXLWQUHWKWEIJMTUIUJ
      VMVEVDXIVTWAVPVSWQVNWLFEIJMVGVHVLVOVL $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A a u x $.  $d A u z $.  $d B a u x $.  $d B u z $.  $d C a u x $.
    $d C u z $.  $d D a u x $.  $d D u z $.  $d F u $.  $d G a u x $.
    $d G u z $.  $d X u x $.  $d X u z $.
    cmprltr2.1 $e |- F = ( z e. X |-> ( ( A G z ) G C ) ) $.
    cmprltr2.2 $e |- E = ( x e. X |-> ( ( B G x ) G D ) ) $.
    cmprltr2.3 $e |- X = ran G $.
    $( Composite of two right and left translations.  No restriction: ` x ` and
       ` z ` can be equal.  (Contributed by FL, 2-Jul-2012.) $)
    cmprltr2 $p |- ( ( G e. GrpOp /\
      ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) -> ( F o. E ) =
         ( x e. X |-> ( ( ( A G B ) G x ) G ( D G C ) ) ) ) $=
      ( vu va wcel co cv cmpt ax-17 cgr wa w3a ccom opreq2 opreq1d cbvmpt eqtri
      weq cmprltr syl6eq ) IUAPCJPDJPUBEJPFJPUBUCHGUDNJCDIQZNRZIQZFEIQZIQZSAJUL
      ARZIQZUOIQZSNBCDEFGHIJKGAJDUQIQZFIQZSNJDUMIQZFIQZSLANOJVAVCORZVAPNTVDVCPA
      TANUIUTVBFIUQUMDIUEUFUGUHMUJNAOJUPUSVDUPPATVDUSPNTNAUIUNURUOIUMUQULIUEUFU
      GUK $.
      $( [2-Jul-2012] $)
  $}


  ${
    rltr.1 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    rltr.2 $e |- X = ran G $.

    ${
      $d X x $.
      $( The domain of a right and left translation.  (Contributed by FL,
         2-Jul-2012.) $)
      rltrdom $p |- dom F = X $=
        ( cv co cvv wcel wral cdm wceq oprex a1i rgen cmpdom mpbi ) BAIZEJZCEJZ
        KLZAFMDNFOUDAFUDUAFLUBCEPQRAFUCDGST $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d X x $.
      $( A right and left translation is a set.  (Contributed by FL,
         2-Jul-2012.) $)
      rltrset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( cgr wcel wfun cdm cvv cv co cmpfun cmpdom2 unsgrp syl5eqel funex
        sylancr ) EIJZDKDLZMJDMJAFBANEOZCEODGPUBUCFMAFUDCDEGQEFHRSMDTUA $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d A x y $.  $d B x y $.  $d G x y $.  $d X x y $.
      $( The range of a right and left translation.  Note that ` A ` and ` B `
         are constant.  (Contributed by FL, 2-Jul-2012.) $)
      rltrran $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) -> ran F = X ) $=
        ( vy wcel w3a co wceq wa grpocl syl111anc cfv syl11anc eqcomd opreq1d
        cgr crn cv wrex cmpran wi simpr1 simpr2 simpl simpr3 3jca ex syl6 eleq1
        cab imbi2d syl5ibrcom r19.23aiv com12 cgn simpl1 eqid grpoinvcl 3adant3
        adantr simpr 3adant2 simpl3 grpoass syl13anc grpolinv opreq2d 3ad2antl1
        cgi grporid 3eqtr2rd grpolid simpl2 grporinv 3eqtrd eqtrd opreq2 eqeq2d
        rcla4ev impbid abbi1dv syl5eq ) EUAJZBFJZCFJZKZDUBIUCZBAUCZELZCELZMZAFU
        DZIUOFAIFWODGUEWKWQIFWKWQWLFJZWQWKWRWPWKWRUFZAFWMFJZWSWPWKWOFJZUFWTWKWH
        WNFJZWJKZXAWTWKXCWTWKNZWHXBWJWTWHWIWJUGZXDWHWIWTXBXEWTWHWIWJUHWTWKUIBWM
        EFHOPWTWHWIWJUJUKULWNCEFHOUMWPWRXAWKWLWOFUNUPUQURUSWKWRWQWKWRNZBEUTQZQZ
        WLCXGQZELZELZFJZWLBXKELZCELZMZWQXFWHXHFJZXJFJZXLWHWIWJWRVAZWKXPWRWHWIXP
        WJBEXGFHXGVBZVCVDVEZXFWHWRXIFJZXQXRWKWRVFZWKYAWRWHWJYAWICEXGFHXSVCVGVEZ
        WLXIEFHOPZXHXJEFHOPXFWLBXHELZXJELZCELZXNXFWLXJCELZEVNQZXJELZCELYGXFYHWL
        XICELZELZWLYIELZWLXFWHWRYAWJYHYLMXRYBYCWHWIWJWRVHZWLXICEFHVIVJXFYIYKWLE
        XFYKYIXFWHWJYKYIMXRYNCYIEXGFHYIVBZXSVKRSVLWHWIWRYMWLMWJWLYIEFHYOVOVMVPX
        FXJYJCEXFWHXQXJYJMXRYDWHXQNYJXJXJYIEFHYOVQSRTXFYJYFCEXFYIYEXJEXFYEYIXFW
        HWIYEYIMXRWHWIWJWRVRZBYIEXGFHYOXSVSRSTTVTXFYFXMCEXFWHWIXPXQYFXMMXRYPXTY
        DBXHXJEFHVIVJTWAWPXOAXKFWMXKMZWOXNWLYQWNXMCEWMXKBEWBTWCWDRULWEWFWG $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d A u x y $.  $d B u x y $.  $d G u x y $.  $d X u x y $.
      $( A right and left translation is a bijection.  (Contributed by FL,
         2-Jul-2012.) $)
      rltrooo $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X )
           -> F : X -1-1-onto-> X ) $=
        ( vy vu cv co wceq wcel w3a wf1o wi cfv wral wa cgr wfn crn weq cvv a1i
        cmpt oprex rgen eqid mptfng sylib rltrran fvmpt2 mpan2 ad2antrl opreq1d
        opreq2 fvmpt ad2antll eqeq12d simpl simprl1 simpl1 simpl2 simprl grpocl
        wb syl111anc simprr simpl3 3jca grporcan syl11anc mpbid simprrl simprrr
        adantl simprl2 grpolcan syl13anc syl6bi pm2.43a r19.21aivv hbmpt1 ax-17
        ex dff1o6f sylibr f1oeq1 syl5ibr ax-mp ) DAFBAKZELZCELZUGZMZEUANZBFNZCF
        NZOZFFDPZQGXAXBWQFFWPPZXAWPFUBZWPUCFMZWMWPRZIKZWPRZMZAIUDZQZIFSAFSZOXCX
        AXDXEXLXAWOUENZAFSZXDXNXAXMAFXMWMFNZWNCEUHZUFUIUFAFWOWPWPUJZUKULABCWPEF
        XQHUMXAXKAIFFXAXOXGFNZTZXKXIXAXSTZXJXTXIWOBXGELZCELZMZXTXJQXTXFWOXHYBXO
        XFWOMZXAXRXOXMYDXPAFWOUEWPXQUNUOUPXRXHYBMXAXOAXGWOYBFWPXJWNYACEWMXGBEUR
        UQXQYACEUHUSUTVAYCXTXJYCXTTZWNYAMZXJYEYCYFYCXTVBYEWRWNFNZYAFNZWTOZYCYFV
        HWRWSWTXSYCVCZXTYIYCXTYGYHWTXTWRWSXOYGWRWSWTXSVDZWRWSWTXSVEZXAXOXRVFBWM
        EFHVGVIXTWRWSXRYHYKYLXAXOXRVJBXGEFHVGVIWRWSWTXSVKVLVRWNYACEFHVMVNVOYEWR
        XOXRWSYFXJVHYJYCXAXOXRVPYCXAXOXRVQWRWSWTXSYCVSWMXGBEFHVTWAVOWGWBWCWGWDV
        LAIJFFWPAJFWOWEJKWPNIWFWHWIFFDWPWJWKWL $.
        $( [2-Jul-2012] $)
    $}
  $}

  $( Compare fnopab2g and fnopab2 . If an antecedent, the condition B e. _V
     must be quantified. If a premisse it needn't. $)

