$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Translations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    trfun.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trinv.1 $e |- X = ran G $.
    ${
      $d A x $.  $d G x $.  $d X x $.
      $( The domain of a right translation.  The term ` A ` is a constant:
         ` x ` is not present.  (Contributed by FL, 21-Jun-2010.) $)
      trdom2 $p |- ( ( G e. GrpOp /\ A e. X ) -> dom F = X ) $=
        ( cgr wcel wa cv co cvv wral cdm wceq oprex a1i ralrimiva cmpdom sylib
        ) DHIBEIJZAKZBDLZMIZAENCOEPUBUEAEUEUBUCEIJUCBDQRSAEUDCFTUA $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d X x $.
      $( A right translation is a set.  (Contributed by FL, 19-Sep-2010.) $)
      trset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( cgr wcel wfun cdm cvv cv cmpfun cmpdom2 unsgrp syl5eqel funex sylancr
        co ) DHIZCJCKZLICLIAEAMZBDTCFNUAUBELAEUCBCDFODEGPQLCRS $.
        $( [19-Sep-2010] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( The range of a right translation.  The term ` A ` is a constant: ` x `
         is not present.  (Contributed by FL, 21-Jun-2010.) $)
      trran2 $p |- ( ( G e. GrpOp /\ A e. X ) -> ran F = X ) $=
        ( vy cgr wcel wa cv co wceq wi grpocl cfv eqid adantr eqcomd wrex rnmpt
        crn cab 3exp com23 impcom eleq1 syl5ibrcom expcom rexlimdv simpll simpr
        imp cgn grpoinvcl syl3anc cgi grpolinv opreq2d grporid adantlr syl13anc
        simplr grpoass 3eqtr4d opreq1 eqeq2d rcla4ev syl2anc ex impbid abbi1dv
        syl5eq ) DIJZBEJZKZCUCHLZALZBDMZNZAEUAZHUDEAHEVTCFUBVQWBHEVQWBVREJZVQWA
        WCAEVSEJZVQWAWCOWDVQKWCWAVTEJZVQWDWEVOVPWDWEOVOWDVPWEVOWDVPWEVSBDEGPUEU
        FUNUGVRVTEUHUIUJUKVQWCWBVQWCKZVRBDUOQZQZDMZEJZVRWIBDMZNZWBWFVOWCWHEJZWJ
        VOVPWCULZVQWCUMZVQWMWCBDWGEGWGRZUPSZVRWHDEGPUQWFVRDURQZDMZVRWHBDMZDMZVR
        WKWFWRWTVRDWFWTWRVQWTWRNWCBWRDWGEGWRRZWPUSSTUTVOWCVRWSNVPVOWCKWSVRVRWRD
        EGXBVATVBWFVOWCWMVPWKXANWNWOWQVOVPWCVDVRWHBDEGVEVCVFWAWLAWIEVSWINVTWKVR
        VSWIBDVGVHVIVJVKVLVMVN $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.  $d G x y z $.  $d X x y z $.
      $( A right translation is a bijection.  The term ` A ` is a constant.
         (Contributed by FL, 21-Jun-2010.) $)
      trooo $p |- ( ( G e. GrpOp /\ A e. X ) -> F : X -1-1-onto-> X ) $=
        ( vy vz wcel wa wfn crn wceq cv cfv wi wral cvv wb cgr w3a co oprex a1i
        wf1o rgen mptfng mpbii trran2 fvmpt2 mpan2 ad2antrl simpr adantl opreq1
        jctir fvmptg syl eqeq12d biimpd grporcan 3exp2 imp3a com23 imp31 sylibd
        ex ralrimivv 3jca hbmpt1 ax-17 dff1o6f f1oeq1 ax-mp fneq1i rneqi eqeq1i
        cmpt fveq1i eqeq12i imbi1i 2ralbii 3anbi123i 3bitr4i sylibr ) DUAJZBEJZ
        KZCELZCMZENZAOZCPZHOZCPZNZWMWONZQZHERAERZUBZEECUFZWIWJWLWTWIWMBDUCZSJZA
        ERZWJXDAEXDWMEJZWMBDUDZUEUGXEWJTWIAEXCCFUHUEUIABCDEFGUJWIWSAHEEWIXFWOEJ
        ZKZWSWIXIKZWQXCWOBDUCZNZWRXJWQXLXJWNXCWPXKXFWNXCNZWIXHXFXDXMXGAEXCSCFUK
        ULUMXJXHXKSJZKZWPXKNXIXOWIXIXHXNXFXHUNWOBDUDUQUOAWOXCXKESCWMWOBDUPFURUS
        UTVAWGWHXIXLWRTZWGXIWHXPWGXFXHWHXPQWGXFXHWHXPWMWOBDEGVBVCVDVEVFVGVHVIVJ
        EEAEXCVSZUFZXQELZXQMZENZWMXQPZWOXQPZNZWRQZHERAERZUBXBXAAHIEEXQAIEXCVKIO
        XQJHVLVMCXQNXBXRTFEECXQVNVOWJXSWLYAWTYFECXQFVPWKXTECXQFVQVRWSYEAHEEWQYD
        WRWNYBWPYCWMCXQFVTWOCXQFVTWAWBWCWDWEWF $.
        $( [31-Dec-2010] $) $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.  $d D y z $.  $d G x y z $.  $d X x y z $.
      trinv.2 $e |- D = ( /g ` G ) $.
      $( The converse of a right translation.  The term ` A ` is a constant.
         (Contributed by FL, 21-Jun-2010.) $)
      trinv $p |- ( ( G e. GrpOp /\ A e. X )
          -> `' F = ( x e. X |-> ( x D A ) ) ) $=
        ( vy vz wcel wa co cmpt wceq cfv wral cvv wfn ccnv cv wf1o trooo adantr
        cgr grpodivcl com23 imp31 jca oprex opreq1 cbvmptv eqtri fvmptg sylancl
        3exp grponpcan eqtrd f1ocnvfv sylc simpr fvmpt2 eqtr4d ralrimiva dff1o4
        eqid wb simprbi syl a1i rgen mptfng mpbii hbmpt1 eqfnfv2f cnveqi fneq1i
        hbcnv anbi1i eqeq1i fveq1i ralbii bibi12i 3imtr4i syl2anc mpbird ) EUFL
        ZBFLZMZDUAZAFAUBZBCNZOZPZWLWKQZWLWNQZPZAFRZWJWRAFWJWLFLZMZWPWMWQXAFFDUC
        ZWMFLZMWMDQZWLPWPWMPXAXBXCWJXBWTABDEFGHUDZUEWHWIWTXCWHWTWIXCWHWTWIXCWLB
        CEFHIUGUQUHUIZUJXAXDWMBENZWLXAXCXGSLXDXGPXFWMBEUKJWMJUBZBENZXGFSDXHWMBE
        ULDAFWLBENZOZJFXIOGAJFXJXIWLXHBEULUMUNUOUPWHWIWTXGWLPZWHWTWIXLWHWTWIXLW
        LBCEFHIURUQUHUIUSFFWMWLDUTVAXAWTWMSLZWQWMPWJWTVBWLBCUKZAFWMSWNWNVGZVCUP
        VDVEWJWKFTZWNFTZWOWSVHZWJXBXPXEXBDFTXPFFDVFVIVJWJXMAFRZXQXMAFXMWTXNVKVL
        XSXQVHWJAFWMWNXOVMVKVNXKUAZFTZXQMXTWNPZWLXTQZWQPZAFRZVHXPXQMXRAKFXTWNAK
        XKAKFXJVOVSAKFWMVOVPXPYAXQFWKXTDXKGVQZVRVTWOYBWSYEWKXTWNYFWAWRYDAFWPYCW
        QWLWKXTYFWBWAWCWDWEWFWG $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A a b x y $.  $d B a b x y $.  $d F x $.  $d G a b x y $.
      $d X a b x y $.
      cmprtr.1 $e |- H = ( x e. X |-> ( x G B ) ) $.
      $( Composite of two right translations.  The terms ` A ` and ` B ` are
         constant.  Don't use.  See ~ cmprtr2 .  (Contributed by FL,
         17-Oct-2010.) $)
      cmprtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( x G ( B G A ) ) ) ) $=
        ( vy va vb wcel cv co wceq wa copab ax-17 cgr w3a crn wss trran2 eqimss
        ccom cmpt syl 3adant2 oprex opreq1 df-mpt eqtri wb eleq1 adantr eqeq12d
        simpr anbi12d cbvopab 3eqtri eqid fopabco simpl1 simpl3 simpl2 syl13anc
        weq grpoass eqeq2d pm5.32da opabbid eqcomi a1i 3eqtrd ) EUANZBGNZCGNZUB
        ZDFUGZAOZGNZKOZWBCEPZBEPZQZRZAKSZWCWDWBCBEPEPZQZRZAKSZAGWJUHZVTFUCZGUDZ
        WAWIQVQVSWPVRVQVSRWOGQWPACFEGJIUEWOGUFUIUJAKLMKGGWELOZBEPZWFFDWIWBCEUKW
        QBEUKWEBEUKWQWEBEULFAGWEUHWCWDWEQRAKSJAKGWEUMUNDAGWBBEPZUHWCWDWSQZRZAKS
        WQGNZMOZWRQZRZLMSHAKGWSUMXAXEAKLMXALTXAMTXEATXEKTALVIZKMVIZRZWCXBWTXDXF
        WCXBUOXGWBWQGUPUQXHWDXCWSWRXFXGUSXFWSWRQXGWBWQBEULUQURUTVAVBWIVCVDUIVTW
        HWLAKVTATVTKTVTWCWGWKVTWCRZWFWJWDXIVQWCVSVRWFWJQVQVRVSWCVEVTWCUSVQVRVSW
        CVFVQVRVSWCVGWBCBEGIVJVHVKVLVMWMWNQVTWNWMAKGWJUMVNVOVP $.
        $( [17-Oct-2010] $)
    $}

    ${
      $d A x y $.  $d B x y $.  $d F y $.  $d G x y $.  $d X x y $.
      cmprtr2.1 $e |- H = ( x e. X |-> ( x G B ) ) $.
      $( Composite of two right translations.  ( ~ cmprtr with a distinct
         variable condition relaxed.)  (Contributed by FL, 1-Jan-2011.) $)
      cmprtr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( x G ( B G A ) ) ) ) $=
        ( vy cgr wcel w3a cv co cmpt opreq1 cbvmptv eqtri ccom cmprtr syl6eq )
        ELMBGMCGMNDFUAKGKOZCBEPZEPZQAGAOZUEEPZQKBCDEFGDAGUGBEPZQKGUDBEPZQHAKGUI
        UJUGUDBERSTIFAGUGCEPZQKGUDCEPZQJAKGUKULUGUDCERSTUBKAGUFUHUDUGUEERSUC $.
        $( [1-Jan-2011] $)
    $}

    ${
      $d A x $.  $d B a u $.  $d F a u $.  $d G x $.  $d X x $.
      $( The image of a set through a translation.  (Contributed by FL,
         30-Dec-2010.) $)
      imtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X ) ->
       ( F " B ) = { a | E. u e. B a = ( F ` u ) } ) $=
        ( cgr wcel cpw w3a cima cv wceq wrex cab wi cfv cdm co cmpfun wa trdom2
        wfun wss pweq eleq2d biimpd eqcoms elpwi syl6 syl dfimafn sylancr eqcom
        3impia rexbii abbii syl6eq ) FKLZCGLZDGMZLZNZEDOZBPEUAZHPZQZBDRZHSZVJVI
        QZBDRZHSVGEUGDEUBZUHZVHVMQAGAPCFUCEIUDVCVDVFVQVCVDUEVPGQZVFVQTACEFGIJUF
        VRVFDVPMZLZVQVFVTTGVPGVPQZVFVTWAVEVSDGVPUIUJUKULDVPUMUNUOUSBHDEUPUQVLVO
        HVKVNBDVIVJURUTVAVB $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A a v x $.  $d B a x $.  $d F a x $.  $d G a v x $.  $d X a x $.
      prsubrtr.1 $e |- H = ( cset ` G ) $.
      $( The product of a subset ` B ` of ` X ` by an element of ` X ` is the
         image of ` B ` by a right translation.  (Contributed by FL,
         30-Dec-2010.) $)
      prsubrtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X )
        -> ( B H { A } ) = ( F " B ) ) $=
        ( va vv cgr wcel cpw cv co wceq wrex wb w3a csn cab cima wa elsn opreq2
        cfv eqeq2d sylbi rexbiia c0 wne snnzg 3ad2ant2 adantr bicomd syl syl5bb
        r19.9rzv cvv wi elelpwi expcom 3ad2ant3 imp oprex fvmpt2 sylancl eqcomd
        bitrd rexbidva abbidv cdm simp1 crn grporndm syl5eq pweq eleq2d 3adant2
        biimpa snelpwg adantl mpbid 3adant3 eqid iscst2 syl3anc imtr 3eqtr4d )
        EMNZBGNZCGOZNZUAZKPZAPZLPZEQZRZLBUBZSZACSZKUCZWQWRDUHZRZACSZKUCCXBFQZDC
        UDWPXDXHKWPXCXGACWPWRCNZUEZXCWQWRBEQZRZXGXCXMLXBSZXKXMXAXMLXBWSXBNWSBRZ
        XAXMTLBUFXOWTXLWQWSBWREUGUIUJUKXKXBULUMZXNXMTWPXPXJWMWLXPWOBGUNUOUPXPXM
        XNXMLXBUTUQURUSXKXLXFWQXKXFXLXKWRGNZXLVANXFXLRWPXJXQWOWLXJXQVBWMXJWOXQW
        RCGVCVDVEVFWRBEVGAGXLVADHVHVIVJUIVKVLVMWPWLCEVNVNZOZNZXBXSNZXIXERWLWMWO
        VOWLWOXTWMWLWOXTWLWNXSCWLGXRRWNXSRWLGEVPXRIEVQVRZGXRVSURVTWBWAWLWMYAWOW
        LWMUEBXRNZYAWLWMYCWLGXRBYBVTWBWMYCYATWLBXRGWCWDWEWFLACXBEFMXRKXRWGJWHWI
        AABCDEGKHIWJWK $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A a x $.  $d B a $.  $d F a $.  $d G a x $.  $d X a x $.
      prsubrtr2.1 $e |- H = ( cset ` G ) $.
      $( The product of a subset ` B ` of ` X ` by an element of ` X ` is the
         image of ` B ` by a right translation.  ( ~ prsubrtr with a distinct
         variable condition relaxed.)  (Contributed by FL, 1-Jan-2011.) $)
      prsubrtr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X )
        -> ( B H { A } ) = ( F " B ) ) $=
        ( va cv co cmpt opreq1 cbvmptv eqtri prsubrtr ) KBCDEFGDAGALZBEMZNKGKLZ
        BEMZNHAKGTUBSUABEOPQIJR $.
        $( [1-Jan-2011] $)
    $}

    ${
      $d A x $.  $d G x $.  $d X x $.
      caytr.1 $e |- H = ( cset ` G ) $.
      $( "It follows that if the entire group is multiplied by any one of the
         symbols, either as further or nearer factor, the effect is simply to
         reproduce the group... ."  Cayley, On the theory of groups, as
         depending on the symbolic equation th^n = 1, 1854.  (it is the
         original paper where the axiomatic definition of a group was given for
         the first time.)  (Contributed by FL, 15-Oct-2012.) $)
      caytr $p |- ( ( G e. GrpOp /\ A e. X ) -> ( X H { A } ) = X ) $=
        ( cgr wcel wa csn co cima crn cpw wceq cvv syl wss ssid wb unsgrp elpwg
        adantr mpbiri prsubrtr2 mpd3an3 wf1o trooo f1ofn fnima trran2 3eqtrd
        wfn ) DJKZBFKZLZFBMENZCFOZCPZFUQURFFQKZUTVARUSVCFFUAZFUBUSFSKZVCVDUCUQV
        EURDFHUDUFFFSUETUGABFCDEFGHIUHUIUSCFUPZVAVBRUSFFCUJVFABCDFGHUKFFCULTFCU
        MTABCDFGHUNUO $.
        $( [15-Oct-2012] $)
    $}

  $}


  ${
    ltrdom.1 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrdom.2 $e |- X = ran G $.
    ${
      $d A x $.  $d G x $.  $d X x $.
      $( The domain of a left translation.  The term ` A ` is a constant.
         (Contributed by FL, 26-Apr-2012.) $)
      ltrdom $p |- ( ( G e. GrpOp /\ A e. X ) -> dom F = X ) $=
        ( cgr wcel wa cv co cvv wral cdm wceq oprex a1i ralrimiva cmpdom sylib
        ) DHIBEIJZBAKZDLZMIZAENCOEPUBUEAEUEUBUCEIJBUCDQRSAEUDCFTUA $.
        $( [26-Apr-2012] $)
    $}

    ${
      $d X x $.
      $( A left translation is a set.  (Contributed by FL, 28-Apr-2012.) $)
      ltrset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( cgr wcel wfun cdm cvv cv cmpfun cmpdom2 unsgrp syl5eqel funex sylancr
        co ) DHIZCJCKZLICLIAEBAMZDTCFNUAUBELAEBUCCDFODEGPQLCRS $.
        $( [28-Apr-2012] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( The range of a left translation.  The term ` A ` is a constant.
         (Contributed by FL, 28-Apr-2012.) $)
      ltrran2 $p |- ( ( G e. GrpOp /\ A e. X ) -> ran F = X ) $=
        ( vy cgr wcel wa crn cv co wceq grpocl cfv eqid adantr eqcomd cab rnmpt
        wrex wi 3expia impcom eleq1 syl5ibrcom expcom rexlimdv simpll grpoinvcl
        cgn simpr syl3anc cgi grpolid adantlr grporinv opreq1d grpoass syl13anc
        simplr 3eqtrd opreq2 eqeq2d rcla4ev syl2anc ex impbid abbi1dv syl5eq )
        DIJZBEJZKZCLHMZBAMZDNZOZAEUCZHUAEAHEVRCFUBVOVTHEVOVTVPEJZVOVSWAAEVQEJZV
        OVSWAUDWBVOKWAVSVREJZVOWBWCVMVNWBWCBVQDEGPUEUFVPVREUGUHUIUJVOWAVTVOWAKZ
        BDUMQZQZVPDNZEJZVPBWGDNZOZVTWDVMWFEJZWAWHVMVNWAUKZVOWKWABDWEEGWERZULSZV
        OWAUNZWFVPDEGPUOWDVPDUPQZVPDNZBWFDNZVPDNZWIVMWAVPWQOVNVMWAKWQVPVPWPDEGW
        PRZUQTURWDWPWRVPDVOWPWROWAVOWRWPBWPDWEEGWTWMUSTSUTWDVMVNWKWAWSWIOWLVMVN
        WAVCWNWOBWFVPDEGVAVBVDVSWJAWGEVQWGOVRWIVPVQWGBDVEVFVGVHVIVJVKVL $.
        $( [28-Apr-2012] $)
    $}

    ${
      $d A a b x z $.  $d G a b x z $.  $d X a b x z $.
      $( A left translation is a bijection.  The term ` A ` is a constant.
         (Contributed by FL, 29-Apr-2012.) $)
      ltrooo $p |- ( ( G e. GrpOp /\ A e. X ) -> F : X -1-1-onto-> X ) $=
        ( va vb vz cv co wceq wcel wa wi wral w3a 3exp 3adant3 cmpt cgr wfn crn
        wf1o cfv weq cvv oprex a1i rgen eqid mptfng sylib ltrran2 simprl grpocl
        com3r adantr com3l imp31 opreq2 fvmptg syl2anc eqcomd simprr 3expia imp
        simp3 adantld jca syl 3eqtrd grpolcan expcom com34 mpbid ralrimivv 3jca
        wb ax-17 dff1o6f sylibr f1oeq1 syl5ibr ax-mp ) CAEBAKZDLZUAZMZDUBNZBENZ
        OZEECUEZPFWMWNWJEEWIUEZWMWIEUCZWIUDEMZHKZWIUFZIKZWIUFZMZHIUGZPZIEQHEQZR
        WOWMWPWQXEWMWHUHNZAEQZWPXGWMXFAEXFWGENBWGDUIUJUKUJAEWHWIWIULZUMUNABWIDE
        XHGUOWMXDHIEEWMWRENZWTENZOZXBXCWMXKXBRZBWRDLZBWTDLZMZXCXLXMWSXAXNWMXKXM
        WSMXBWMXKOZWSXMXPXIXMENZWSXMMWMXIXJUPWKWLXKXQXKWKWLXQXIWKWLXQPPXJWKWLXI
        XQWKWLXIXQBWRDEGUQSURUSUTVAAWRWHXMEEWIWGWRBDVBXHVCVDVETWMXKXBVIXLXJXNEN
        ZOZXAXNMWMXKXSXBXPXJXRWMXIXJVFWMXKXRWMXJXRXIWKWLXJXRBWTDEGUQVGVJVHVKTAW
        TWHXNEEWIWGWTBDVBXHVCVLVMWMXKXOXCVTZXBWKWLXKXTXKWKWLXTXIXJWKWLXTPPXIXJW
        LWKXTXIXJWLWKXTPWKXIXJWLRXTWRWTBDEGVNVOSVPVHUTVATVQSVRVSHIJEEWIJKWINZHW
        AYAIWAWBWCEECWIWDWEWF $.
        $( [29-Apr-2012] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( Left translation expressed as a composite.  (Contributed by FL,
         3-Jul-2012.) $)
      ltrcmp $p |- ( ( G e. GrpOp /\ A e. X ) ->
        F = ( G o. { <. x , y >. | ( x e. X /\ y = <. A , x >. ) } ) ) $=
        ( cgr wcel wa cv cop cxp wral wfn wceq copab ccom opelxpi ralrimiva wfo
        adantll grpofo fofn syl adantr eqid co cmpt df-mpt df-opr eqeq2i anbi2i
        cfv opabbii 3eqtri fnopabco2b syl2anc ) EIJZCFJZKZCALZMZFFNZJZAFOEVEPZD
        EVCFJZBLZVDQKABRZSQVBVFAFVAVHVFUTCVCFFTUCUAUTVGVAUTVEFEUBVGEFHUDVEFEUEU
        FUGABFVDVEVJDEVJUHDAFCVCEUIZUJVHVIVKQZKZABRVHVIVDEUOZQZKZABRGABFVKUKVMV
        PABVLVOVHVKVNVICVCEULUMUNUPUQURUS $.
        $( [3-Jul-2012] $)
    $}

    ${
      $d A x y z $.  $d F z $.  $d G x y z $.  $d N x y z $.  $d X x y z $.
      ltrinvlem.2 $e |- D = ( /g ` G ) $.
      ltrinvlem.3 $e |- N = ( inv ` G ) $.
      $( The converse of a left translation.  The term ` A ` is a constant.
         (Contributed by FL, 30-Apr-2012.) $)
      ltrinvlem $p |- ( ( G e. GrpOp /\ A e. X )
          -> `' F = ( x e. X |-> ( ( N ` A ) G x ) ) ) $=
        ( vy vz wcel wa cfv cv co cmpt wceq ccnv wral wf1o ltrooo adantr simpll
        cgr grpoinvcl simpr grpocl syl3anc simplr opreq2 cbvmptv fvmptg syl2anc
        jca eqtri w3a grpoass eqcomd syl13anc cgi eqid grporinv opreq1d grpolid
        adantlr eqtrd 3eqtrd f1ocnvfv sylc fvmpt2 eqtr4d ralrimiva wfn wb f1ofn
        f1ocnv 3syl syldan syl wal wi hbmpt1 hbcnv a1i cnveq hbth albid 3imtr4d
        eleq2d ax-mp eqfnfv2f mpbird ) EUGNZBGNZOZDUAZAGBFPZAQZERZSZTZXAWSPZXAX
        CPZTZAGUBZWRXGAGWRXAGNZOZXEXBXFXJGGDUCZXBGNZOXBDPZXATXEXBTXJXKXLWRXKXIA
        BDEGHIUDZUEXJWPWTGNZXIXLWPWQXIUFZWRXOXIBEFGIKUHZUEZWRXIUIZWTXAEGIUJUKZU
        QXJXMBXBERZBWTERZXAERZXAXJXLYAGNZXMYATXTXJWPWQXLYDXPWPWQXIULZXTBXBEGIUJ
        UKLXBBLQZERZYAGGDYFXBBEUMDAGBXAERZSZLGYGSHALGYHYGXAYFBEUMUNURUOUPXJWPWQ
        XOXIYAYCTXPYEXRXSWPWQXOXIUSOYCYABWTXAEGIUTVAVBXJYCEVCPZXAERZXAXJYBYJXAE
        WRYBYJTXIBYJEFGIYJVDZKVEUEVFWPXIYKXATWQXAYJEGIYLVGVHVIVJGGXBXADVKVLXJXI
        XLXFXBTXSXTAGXBGXCXCVDZVMUPVNVOWRWSGVPZXCGVPZXDXHVQWRXKGGWSUCYNXNGGDVSG
        GWSVRVTWRGGXCUCZYOWPWQXOYPXQAWTXCEGYMIUDWAGGXCVRWBAMGWSXCDYITZMQZWSNZYS
        AWCZWDHYQYRYIUAZNZUUBAWCZYSYTUUBUUCWDYQAMYIAMGYHWEWFWGYQWSUUAYRDYIWHWLZ
        YQYSUUBAYQAHWIUUDWJWKWMAMGXBWEWNUPWO $.
        $( [30-Apr-2012] $)
    $}

    ${
      $d A v x y z $.  $d B v w x y z $.  $d F z $.  $d G v w x y z $.
      $d X v w x y z $.
      cmpltr.2 $e |- H = ( x e. X |-> ( B G x ) ) $.
      $( Composite of two left translations.  The terms ` A ` and ` B ` are
         constant.  Don't use.  See ~ cmpltr2 .  (Contributed by FL,
         2-Jul-2012.) $)
      cmpltr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( ( A G B ) G x ) ) ) $=
        ( vz wcel cv co cmpt wceq syl oprex opreq2 cbvmptv cgr w3a ccom crn wss
        wa ltrran2 eqimss 3adant2 eqtri eqid fmptco grpoass 3exp2 3imp1 opreq2d
        mpteq2dva syl6req eqtrd ) EUALZBGLZCGLZUBZDFUCZKGBCKMZENZENZOZAGBCENAMZ
        ENZOZVCFUDZGUEZVDVHPUTVBVMVAUTVBUFVLGPVMACFEGJIUGVLGUHQUIKAGGVFBVIENVGF
        DVHCVEERBVIERBVFERVIVFBESFAGCVIENZOKGVFOJAKGVNVFVIVECESZTUJHVHUKULQVCVK
        AGBVNENZOVHVCAGVJVPUTVAVBVIGLZVJVPPZUTVAVBVQVRBCVIEGIUMUNUOUQAKGVPVGVIV
        EPVNVFBEVOUPTURUS $.
        $( [2-Jun-2014] $) $( [2-Jul-2012] $)

    $}

    ${
      $d A x y $.  $d B x y $.  $d G x y $.  $d X x y $.
      cmpltr2.2 $e |- H = ( x e. X |-> ( B G x ) ) $.
      $( Composite of two left translations.  The terms ` A ` and ` B ` are
         constant.  (Contributed by FL, 2-Jul-2012.) $)
      cmpltr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( ( A G B ) G x ) ) ) $=
        ( vy cgr wcel w3a co cv cmpt opreq2 cbvmptv eqtri ccom cmpltr syl6eq )
        ELMBGMCGMNDFUAKGBCEOZKPZEOZQAGUDAPZEOZQKBCDEFGDAGBUGEOZQKGBUEEOZQHAKGUI
        UJUGUEBERSTIFAGCUGEOZQKGCUEEOZQJAKGUKULUGUECERSTUBKAGUFUHUEUGUDERSUC $.
        $( [2-Jul-2012] $)
    $}
  $}

  ${
    $d z A $.  $d y z B $.  $d x F $.  $d y z G $.  $d x z X $.  $d y z X $.
    reptertr2.2 $e |- F = ( y e. X |-> ( y G B ) ) $.
    reptertr2.3 $e |- H = ( x e. X |-> A ) $.
    reptertr2.4 $e |- A e. _V $.
    $( Replacement of ` x ` by the term ` A ` in a translation. ` B ` is a
       constant.  Note that ` x ` and ` y ` can be the same.  (Contributed by
       FL, 11-Jun-2012.) $)
    reptertr2 $p |- ( A. x e. X A e. X
      -> ( F o. H ) = ( x e. X |-> ( A G B ) ) ) $=
      ( vz wcel wral crn co cmpt cv oprex opreq1 wss ccom wceq fmpt frn cbvmptv
      wf sylbi eqtri eqid fmptco syl ) CHMAHNZGOHUAZEGUBAHCDFPZQZUCUMHHGUGUNAHH
      CGJUDHHGUEUHALHHCLRZDFPZUOGEUPKUQDFSCDFSUQCDFTJEBHBRZDFPZQLHURQIBLHUTURUS
      UQDFTUFUIUPUJUKUL $.
      $( [11-Jun-2012] $)
  $}

  ${
    $d z A $.  $d y z B $.  $d x F $.  $d y z G $.  $d y z X $.  $d x z X $.
    reptertr3.2 $e |- F = ( y e. X |-> ( B G y ) ) $.
    reptertr3.3 $e |- H = ( x e. X |-> A ) $.
    reptertr3.4 $e |- A e. _V $.
    $( Replacement of ` x ` by the term ` A ` in a translation. ` B ` is a
       constant.  Note that ` x ` and ` y ` can be the same.  (Contributed by
       FL, 2-Jul-2012.) $)
    reptertr3 $p |- ( A. x e. X A e. X
      -> ( F o. H ) = ( x e. X |-> ( B G A ) ) ) $=
      ( vz wcel wral crn co cmpt cv oprex opreq2 wss ccom wceq fmpt frn cbvmptv
      wf sylbi eqtri eqid fmptco syl ) CHMAHNZGOHUAZEGUBAHDCFPZQZUCUMHHGUGUNAHH
      CGJUDHHGUEUHALHHCDLRZFPZUOGEUPKDUQFSDCFSUQCDFTJEBHDBRZFPZQLHURQIBLHUTURUS
      UQDFTUFUIUPUJUKUL $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d z A $.  $d y z B $.  $d y z C $.  $d y z G $.  $d y z X $.  $d x z X $.
    $d x F $.
    reptertr4.2 $e |- F = ( y e. X |-> ( ( B G y ) G C ) ) $.
    reptertr4.3 $e |- H = ( x e. X |-> A ) $.
    reptertr4.4 $e |- A e. _V $.
    $( Replacement of ` x ` by the term ` A ` in a translation. ` B ` and ` C `
       are constant.  (Contributed by FL, 2-Jul-2012.) $)
    reptertr4 $p |- ( A. x e. X A e. X
      -> ( F o. H ) = ( x e. X |-> ( ( B G A ) G C ) ) ) $=
      ( vz co cmpt wceq cv oprex opreq2 opreq1d wcel wral crn wss ccom fmpt frn
      wf sylbi cbvmptv eqtri eqid fmptco syl ) CIUAAIUBZHUCIUDZFHUEAIDCGNZEGNZO
      ZPUOIIHUHUPAIICHKUFIIHUGUIAMIICDMQZGNZEGNZURHFUSLVAEGRUQEGRUTCPVAUQEGUTCD
      GSTKFBIDBQZGNZEGNZOMIVBOJBMIVEVBVCUTPVDVAEGVCUTDGSTUJUKUSULUMUN $.
      $( [2-Jul-2012] $)
  $}


  ${
    $d A x y $.  $d B x y $.  $d E x $.  $d G x y $.  $d X x y $.
    cmperltr.1 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    cmperltr.2 $e |- E = ( y e. X |-> ( A G y ) ) $.
    cmperltr.3 $e |- H = ( x e. X |-> ( x G B ) ) $.
    cmperltr.4 $e |- X = ran G $.
    $( A right and left translation expressed as a composite.  Note that ` x `
       and ` y ` can't be the same.  (Contributed by FL, 2-Jul-2012.) $)
    cmperltr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X )
      -> F = ( E o. H ) ) $=
      ( cgr wcel w3a cv co cmpt wceq grpoass 3exp2 com34 3imp1 mpteq2dva syl5eq
      ccom wral wa wi grpocl 3exp com23 ralrimiv 3adant2 oprex reptertr3 eqtr4d
      imp syl ) GNOZCIOZDIOZPZFAICAQZDGRZGRZSZEHUGZVDFAICVEGRDGRZSVHJVDAIVJVGVA
      VBVCVEIOZVJVGTZVAVBVKVCVLVAVBVKVCVLCVEDGIMUAUBUCUDUEUFVDVFIOZAIUHZVIVHTVA
      VCVNVBVAVCUIVMAIVAVCVKVMUJVAVKVCVMVAVKVCVMVEDGIMUKULUMUSUNUOABVFCEGHIKLVE
      DGUPUQUTUR $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.  $d F x $.  $d G x y $.
    $d X x y $.
    cmprltr.1 $e |- F = ( y e. X |-> ( ( A G y ) G C ) ) $.
    cmprltr.2 $e |- E = ( x e. X |-> ( ( B G x ) G D ) ) $.
    cmprltr.3 $e |- X = ran G $.
    $( Composite of two right and left translations.  Note that ` x ` and ` y `
       can't be the same.  See ~ cmprltr2 for a more general version.
       (Contributed by FL, 2-Jul-2012.) $)
    cmprltr $p |- ( ( G e. GrpOp /\
      ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) -> ( F o. E ) =
         ( x e. X |-> ( ( ( A G B ) G x ) G ( D G C ) ) ) ) $=
      ( wcel wa w3a co wceq wi grpocl cgr ccom cv cmpt wral simpl1 adantr simpr
      syl3anc 3exp1 com34 3imp ralrimiv 3exp com3r adantl com13 com12 reptertr4
      oprex syl simpl2l adantld 3adant3 simpl3r grpoass eqcomd syl13anc simpl2r
      imp opreq1d eqtrd imp32 simpl3l mpteq2dva ) IUANZCJNZDJNZOZEJNZFJNZOZPZHG
      UBZAJCDAUCZIQZFIQZIQZEIQZUDZAJCDIQZWEIQZFEIQIQZUDWCWGJNZAJUEZWDWJRVPVSWBW
      OVSVPWBWOSZVRVPWPSVQWBVPVRWOWAVPVRWOSSVTVPVRWAWOVPVRWAWOVPVRWAPWNAJVPVRWA
      WEJNZWNSVPVRWQWAWNVPVRWQWAWNVPVRWQPZWAOVPWFJNZWAWNVPVRWQWAUFWRWSWADWEIJMT
      ZUGWRWAUHWFFIJMTUIUJUKULUMUNUOUPUQUPURULABWGCEHIGJKLWFFIUTUSVAWCAJWIWMWCW
      QOZWIWLFIQZEIQZWMXAWHXBEIXAWHCWFIQZFIQZXBXAVPVQWSWAWHXERVPVSWBWQUFZVQVRVP
      WBWQVBZWCWQWSVPVSWQWSSZWBVPVSXHVPVRXHVQVPVRWQWSWTUNVCVJVDVJVTWAVPVSWQVEZV
      PVQWSWAPOXEWHCWFFIJMVFVGVHXAXDWLFIXAWLXDXAVPVQVRWQWLXDRXFXGVQVRVPWBWQVIWC
      WQUHCDWEIJMVFVHVGVKVLVKXAVPWLJNZWAVTXCWMRXFWCWQXJVPVSWQXJSZWBVPVQVRXKVPVQ
      VRWQXJVPVQVRPZWQOVPWKJNZWQXJVPVQVRWQUFXLXMWQCDIJMTUGXLWQUHWKWEIJMTUIUJVMV
      DVJXIVTWAVPVSWQVNWLFEIJMVFVHVLVOVL $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A a u x $.  $d A u z $.  $d B a u x $.  $d B u z $.  $d C a u x $.
    $d C u z $.  $d D a u x $.  $d D u z $.  $d F u $.  $d G a u x $.
    $d G u z $.  $d X u x $.  $d X u z $.
    cmprltr2.1 $e |- F = ( z e. X |-> ( ( A G z ) G C ) ) $.
    cmprltr2.2 $e |- E = ( x e. X |-> ( ( B G x ) G D ) ) $.
    cmprltr2.3 $e |- X = ran G $.
    $( Composite of two right and left translations.  No restriction: ` x ` and
       ` z ` can be equal.  (Contributed by FL, 2-Jul-2012.) $)
    cmprltr2 $p |- ( ( G e. GrpOp /\
      ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) -> ( F o. E ) =
         ( x e. X |-> ( ( ( A G B ) G x ) G ( D G C ) ) ) ) $=
      ( vu va wcel co cv cmpt ax-17 cgr wa w3a ccom opreq2 opreq1d cbvmpt eqtri
      weq cmprltr syl6eq ) IUAPCJPDJPUBEJPFJPUBUCHGUDNJCDIQZNRZIQZFEIQZIQZSAJUL
      ARZIQZUOIQZSNBCDEFGHIJKGAJDUQIQZFIQZSNJDUMIQZFIQZSLANOJVAVCORZVAPNTVDVCPA
      TANUIUTVBFIUQUMDIUEUFUGUHMUJNAOJUPUSVDUPPATVDUSPNTNAUIUNURUOIUMUQULIUEUFU
      GUK $.
      $( [2-Jul-2012] $)
  $}


  ${
    rltr.1 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    rltr.2 $e |- X = ran G $.

    ${
      $d X x $.
      $( The domain of a right and left translation.  (Contributed by FL,
         2-Jul-2012.) $)
      rltrdom $p |- dom F = X $=
        ( cv co cvv wcel wral cdm wceq oprex a1i rgen cmpdom mpbi ) BAIZEJZCEJZ
        KLZAFMDNFOUDAFUDUAFLUBCEPQRAFUCDGST $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d X x $.
      $( A right and left translation is a set.  (Contributed by FL,
         2-Jul-2012.) $)
      rltrset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( cgr wcel wfun cdm cvv cv co cmpfun cmpdom2 unsgrp syl5eqel funex
        sylancr ) EIJZDKDLZMJDMJAFBANEOZCEODGPUBUCFMAFUDCDEGQEFHRSMDTUA $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d A x y $.  $d B x y $.  $d G x y $.  $d X x y $.
      $( The range of a right and left translation.  Note that ` A ` and ` B `
         are constant.  (Contributed by FL, 2-Jul-2012.) $)
      rltrran $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) -> ran F = X ) $=
        ( vy wcel w3a co wceq wa grpocl syl3anc cfv syl2anc eqcomd opreq1d wrex
        cgr crn cv cab rnmpt wi simpr1 simpr2 simpl simpr3 3jca ex eleq1 imbi2d
        syl6 syl5ibrcom rexlimiv com12 cgn simpl1 eqid grpoinvcl 3adant3 adantr
        simpr 3adant2 simpl3 grpoass syl13anc grpolinv opreq2d grporid 3eqtr2rd
        cgi 3ad2antl1 grpolid simpl2 grporinv 3eqtrd eqtrd opreq2 eqeq2d impbid
        rcla4ev abbi1dv syl5eq ) EUBJZBFJZCFJZKZDUCIUDZBAUDZELZCELZMZAFUAZIUEFA
        IFWODGUFWKWQIFWKWQWLFJZWQWKWRWPWKWRUGZAFWMFJZWSWPWKWOFJZUGWTWKWHWNFJZWJ
        KZXAWTWKXCWTWKNZWHXBWJWTWHWIWJUHZXDWHWIWTXBXEWTWHWIWJUIWTWKUJBWMEFHOPWT
        WHWIWJUKULUMWNCEFHOUPWPWRXAWKWLWOFUNUOUQURUSWKWRWQWKWRNZBEUTQZQZWLCXGQZ
        ELZELZFJZWLBXKELZCELZMZWQXFWHXHFJZXJFJZXLWHWIWJWRVAZWKXPWRWHWIXPWJBEXGF
        HXGVBZVCVDVEZXFWHWRXIFJZXQXRWKWRVFZWKYAWRWHWJYAWICEXGFHXSVCVGVEZWLXIEFH
        OPZXHXJEFHOPXFWLBXHELZXJELZCELZXNXFWLXJCELZEVOQZXJELZCELYGXFYHWLXICELZE
        LZWLYIELZWLXFWHWRYAWJYHYLMXRYBYCWHWIWJWRVHZWLXICEFHVIVJXFYIYKWLEXFYKYIX
        FWHWJYKYIMXRYNCYIEXGFHYIVBZXSVKRSVLWHWIWRYMWLMWJWLYIEFHYOVMVPVNXFXJYJCE
        XFWHXQXJYJMXRYDWHXQNYJXJXJYIEFHYOVQSRTXFYJYFCEXFYIYEXJEXFYEYIXFWHWIYEYI
        MXRWHWIWJWRVRZBYIEXGFHYOXSVSRSTTVTXFYFXMCEXFWHWIXPXQYFXMMXRYPXTYDBXHXJE
        FHVIVJTWAWPXOAXKFWMXKMZWOXNWLYQWNXMCEWMXKBEWBTWCWERUMWDWFWG $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d A u x y $.  $d B u x y $.  $d G u x y $.  $d X u x y $.
      $( A right and left translation is a bijection.  (Contributed by FL,
         2-Jul-2012.) $)
      rltrooo $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X )
           -> F : X -1-1-onto-> X ) $=
        ( vy vu cv co wceq wcel w3a wf1o wi cfv wral wa cgr wfn crn weq cvv a1i
        cmpt oprex rgen eqid mptfng sylib rltrran fvmpt2 mpan2 ad2antrl opreq1d
        opreq2 fvmpt ad2antll eqeq12d simpl simprl1 simpl1 simpl2 simprl grpocl
        syl3anc simprr simpl3 3jca adantl syl2anc mpbid simprrl simprrr simprl2
        wb grporcan grpolcan syl13anc ex syl6bi pm2.43a ralrimivv ax-17 dff1o6f
        hbmpt1 sylibr f1oeq1 syl5ibr ax-mp ) DAFBAKZELZCELZUGZMZEUANZBFNZCFNZOZ
        FFDPZQGXAXBWQFFWPPZXAWPFUBZWPUCFMZWMWPRZIKZWPRZMZAIUDZQZIFSAFSZOXCXAXDX
        EXLXAWOUENZAFSZXDXNXAXMAFXMWMFNZWNCEUHZUFUIUFAFWOWPWPUJZUKULABCWPEFXQHU
        MXAXKAIFFXAXOXGFNZTZXKXIXAXSTZXJXTXIWOBXGELZCELZMZXTXJQXTXFWOXHYBXOXFWO
        MZXAXRXOXMYDXPAFWOUEWPXQUNUOUPXRXHYBMXAXOAXGWOYBFWPXJWNYACEWMXGBEURUQXQ
        YACEUHUSUTVAYCXTXJYCXTTZWNYAMZXJYEYCYFYCXTVBYEWRWNFNZYAFNZWTOZYCYFVRWRW
        SWTXSYCVCZXTYIYCXTYGYHWTXTWRWSXOYGWRWSWTXSVDZWRWSWTXSVEZXAXOXRVFBWMEFHV
        GVHXTWRWSXRYHYKYLXAXOXRVIBXGEFHVGVHWRWSWTXSVJVKVLWNYACEFHVSVMVNYEWRXOXR
        WSYFXJVRYJYCXAXOXRVOYCXAXOXRVPWRWSWTXSYCVQWMXGBEFHVTWAVNWBWCWDWBWEVKAIJ
        FFWPAJFWOWHJKWPNIWFWGWIFFDWPWJWKWL $.
        $( [2-Jul-2012] $)
    $}
  $}

  $( Compare fnopab2g and fnopab2 . If an antecedent, the condition B e. _V
     must be quantified. If a premisse it needn't. $)

