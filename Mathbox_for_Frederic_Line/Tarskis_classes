$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Tarski's classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c tarskiMap $.
  $( A function whose value is the smallest Tarski's classe
     containing a particular set. $)

  $( Extend class definition to include the map whose value is the smallest
     Tarski's class. $)
  ctarskim $a class tarskiMap $.

  $( The parts of an element of a Tarski's class are Tarski's classes.
     (Contributed by FL, 30-Dec-2010.) $)
  elincin $p |- ( ( T e. Tarski /\ A e. T /\ B C_ A ) -> B e. T ) $=
    ( ctsk wcel wss w3a cvv ssexg ancoms 3adant1 cpw wi elpwg biimprcd 3ad2ant3
    wa tskpwss ssel2 a1d ex syl 3adant3 syld mp2d ) CDEZACEZBAFZGZBHEZUJBCEZUGU
    HUJUFUHUGUJBACIJKZULUIUJBALZEZUJUKMZUHUFUJUNMUGUJUNUHBAHNOPUFUGUNUOMZUHUFUG
    QUMCFZUPACRUQUNUOUQUNQUKUJUMCBSTUAUBUCUDUE $.
    $( [30-Dec-2010] $)

  $( The intersection of two elements of a Tarski's class belongs to the
     class.  (Contributed by FL, 30-Dec-2010.) $)
  inacint $p |- ( ( T e. Tarski /\ A e. T /\ B e. T ) -> ( A i^i B ) e. T ) $=
    ( ctsk wcel w3a cpw cin wss tskpwss 3adant3 ssid mpbiri 3ad2ant2 inpws1 syl
    elpwg sseldd ) CDEZACEZBCEZFZAGZCABHZSTUCCIUAACJKUBAUCEZUDUCETSUEUATUEAAIAL
    AACQMNABAOPR $.
    $( [30-Dec-2010] $)

  $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class is either
     equipotent to the class or an element of the class.  (Contributed by FL,
     17-Apr-2011.) $)
  tarax3a $p |- ( ( T e. Tarski /\ A C_ T ) ->
      ( A ~~ T \/ A e. T ) ) $=
    ( ctsk wcel wss wa cen wbr wo wi cvv ssexg elpwg tsken expcom syl6bir com13
    cpw imp syl5com ancoms pm2.43i ) BCDZABEZFZABGHABDIZUDUCUEUFJUDUCFAKDZUEUFA
    BCLUCUDUGUFJUGUDUCUFUGUDABRDZUCUFJABKMUCUHUFABNOPQSTUAUB $.
    $( [17-Apr-2011] $)

  $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class non
     equipotent to the class is an element of the class.  (Contributed by FL,
     22-Feb-2011.) $)
  tarax3b $p |- ( ( T e. Tarski /\ A e. ~P T /\ -. A ~~ T ) ->
       A e. T ) $=
    ( ctsk wcel cpw cen wbr wn wa tsken ord 3impia ) BCDZABEDZABFGZHABDZMNIOPAB
    JKL $.
    $( [22-Feb-2011] $)

  $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class non
     equipotent to the class is an element of the class.  (Contributed by FL,
     22-Feb-2011.) $)
  tarax3c $p |- ( ( T e. Tarski /\ A C_ T /\ -. A ~~ T ) ->
       A e. T ) $=
    ( ctsk wcel wss cen wbr wn w3a cpw cvv wb ssexg ancoms 3adant3 elpwg bicomd
    3anbi2d syl tarax3b syl6bi pm2.43i ) BCDZABEZABFGHZIZABDZUFUFUCABJDZUEIZUGU
    FAKDZUFUILUCUDUJUEUDUCUJABCMNOUJUDUHUCUEUJUHUDABKPQRSABTUAUB $.
    $( [22-Feb-2011] $)

  $( The cardinal of an element of a Tarski's class belongs to the cardinal of
     the Tarski's class.  JFM CLASSES2 th. 1 (Contributed by FL,
     20-Apr-2011.) $)
  tarax3d3 $p |- ( ( T e. Tarski /\ A e. T )
    -> ( card ` A ) e. ( card ` T ) ) $=
    ( ctsk wcel wa ccrd cfv csdm wbr tsksdom wb cardsdom ancoms mpbird ) BCDZAB
    DZEAFGBFGDZABHIZABJPOQRKABBCLMN $.
    $( [20-Apr-2011] $)


  $( The cardinal of an element of a Tarski's class is strictly less than the
     cardinal of the Tarski's class.  JFM CLASSES2 th. 1 (Contributed by FL,
     20-Apr-2011.) $)
  tarax3d4 $p |- ( ( T e. Tarski /\ A e. T )
    -> ( card ` A ) ~< ( card ` T ) ) $=
    ( ctsk wcel wa ccrd cfv csdm wbr tarax3d3 cardidm a1i eleq12d mpbird cvv wb
    wceq fvex pm3.2i cardsdom syl mpbid ) BCDABDEZAFGZFGZBFGZFGZDZUDUFHIZUCUHUD
    UFDABJUCUEUDUGUFUEUDQUCAKLUGUFQUCBKLMNUCUDODZUFODZEZUHUIPULUCUJUKAFRBFRSLUD
    UFOOTUAUB $.
    $( [20-Apr-2011] $)

  $( An element of a transitive Tarski's class is a part of the class.  JFM
     CLASSES2 th. 48 (Contributed by FL, 30-Dec-2010.) $)
  elcartr $p |- ( ( T e. Tarski /\ Tr T /\ A e. T ) -> A C_ T ) $=
    ( wtr wcel wss ctsk trss imp 3adant1 ) BCZABDZABEZBFDJKLBAGHI $.
    $( [30-Dec-2010] $)

  $( A transitive element of a Tarski's class is a part of the class.  JFM
     CLASSES2 th. 8 (Contributed by FL, 22-Feb-2011.) $)
  tartrel $p |- ( ( T e. Tarski /\ Tr A /\ A e. T ) -> A C_ T ) $=
    ( ctsk wcel wtr wss wi cpw dftr4 wa tskpwss sstr2 syl5com com3r sylbi com12
    ex 3imp ) BCDZAEZABDZABFZTSUAUBGZTAAHZFZSUCGAISUAUEUBSUAUEUBGSUAJUDBFUEUBAB
    KAUDBLMQNOPR $.
    $( [22-Feb-2011] $)

  $( If an element of a Tarski's class is an ordinal number, it is a part of
     the class.  JFM CLASSES2 th. 6 (partly) (Contributed by FL,
     22-Feb-2011.) $)
  tartord $p |- ( ( T e. Tarski /\ Ord A /\ A e. T ) -> A C_ T ) $=
    ( ctsk wcel word wss wi wtr cep wwe wa df-ord tartrel 3exp com12 sylbi 3imp
    adantr ) BCDZAEZABDZABFZTSUAUBGZTAHZAIJZKSUCGZALUDUFUESUDUCSUDUAUBABMNORPOQ
    $.
    $( [22-Feb-2011] $)

  ${
    $d T t $.  $d t x $.
    $( A non empty Tarski class is infinite.  (Contributed by FL,
       22-Feb-2011.) $)
    tclinf $p |- ( ( T e. Tarski /\ T =/= (/) ) -> om ~<_ T ) $=
      ( vt vx ctsk wcel c0 wne com cdom cv wa wi wceq eleq1 neeq1 anbi12d breq2
      wbr imbi12d vex cuni wss simpr wal cpw pwid elunii sylancr adantr alrimiv
      tskpw ex dfss2 sylibr dominf syl2anc vtoclg anabsi5 ) ADEZAFGZHAIRZBJZDEZ
      VBFGZKZHVBIRZLUSUTKZVALBADVBAMZVEVGVFVAVHVCUSVDUTVBADNVBAFOPVBAHIQSVEVDVB
      VBUAZUBZVFVCVDUCVECJZVBEZVKVIEZLZCUDVJVEVNCVCVNVDVCVLVMVCVLKVKVKUEZEVOVBE
      VMVKCTUFVKVBUKVKVOVBUGUHULUIUJCVBVIUMUNVBBTUOUPUQUR $.
      $( [22-Feb-2011] $)
  $}

  ${
    $d A a b $.  $d B b $.  $d T a b $.
    $( The cross product of two elements of a transitive Tarski's class is an
       element of the class.  JFM CLASSES2 th. 67 (partly).  (Contributed by
       FL, 15-Apr-2011.) $)
    cptarc $p |- ( ( T e. Tarski /\ Tr T /\ ( A e. T /\ B e. T ) )
    -> ( A X. B ) e. T ) $=
      ( wcel wa cxp wi wceq c0 w3a adantr com wbr csdm imp 3adant1 3exp pm2.43i
      syl cvv va vb ctsk wtr xpeq1 eleq1d imbi2d xpeq2 wne ne0i eqtr mpan2 tsk0
      cv xp0 ancoms eleq1 syl5ibrcom 3adant2 com12 3syl cdom df-ne cen infxpabs
      wn vex tsksdom expcom wss elcartr tskxpss ensdomtr tskssel 3ad2ant3 com3r
      ex com24 com4l com14 com4r anabsi5 com13 3adant3l syl5com 3adant3r sylbir
      syl6 domtri2 mp2an sdomdom domtr xp0r syl5eqel syl5ibr a1i12 xpcomen entr
      mpd wb simpl3 simp2l3 simp1 simpr adantl ad2antrr syl2anc syl3anc sylancr
      com3l pm2.61ine a1dd sylc sylbi pm2.61i omex simprl3 3jca sdomdomtr breq1
      0sdomg biimpar a1i sylancl anim2i ax-mp 3imp1 3exp1 cptwff simp3l3 tclinf
      a1d syl5 mpan2d impcom vtocl2ga 3imp ) CUCDZCUDZACDBCDEZABFZCDZYTYRYSUUBY
      RYSUAUNZUBUNZFZCDZGZGZYRYSAUUDFZCDZGZGYRYSUUBGZGUAUBABCCUUCAHZUUGUUKYRUUM
      UUFUUJYSUUMUUEUUICUUCAUUDUEUFUGUGUUDBHZUUKUULYRUUNUUJUUBYSUUNUUIUUACUUDBA
      UHUFUGUGUUCCDZUUDCDZUUHUUOCIUIZUUOUUPEZUUHGCUUCUJUUQUURYRYSUUFUUDIHZUUQUU
      RYRJZYSEZUUFGZUUSUUEUUCIFZHZUUEIHZUVBUUDIUUCUHUVDUVCIHUVEUUCUOUUEUVCIUKUL
      UVAUVEUUFUUTUVEUUFGZYSUUQYRUVFUURUUQYREZUUFUVEICDZYRUUQUVHCUMUPZUUEICUQUR
      USKUTVALUUCVBMZUUSVFZUVBGZUUDUUCVBMZUVJUVLGZUVKUVJUVMUVBUVKUUDIUIZUVJUVMU
      VBGZGUUDIVCZUVJUVOUVPUVJUVOUVMUVBUVJUVOUVMJUUEUUCVDMZUVAUUFUUCUUDUAVGZUBV
      GZVEUVAUUCCNMZUVRUUFGZUUTUWAYSUURYRUWAUUQUURYRUWAUUOYRUWAGUUPYRUUOUWAUUCC
      VHVIKOPKUVAUWAUWBGZUUTYSUVAUWCGZUURYRYSUWDGZUUQUURYRUWEYRYSUURUWDYRYSUURU
      WDYRYSUURJZUWDYRYSUUOUWFUWDGUUPYRYSUUOJZUUCCVJZUWFUWDUUCCVKZYRYSUUPUWHUWD
      GZUUOYRYSUUPJZUUDCVJZUWJUUDCVKZUVAUWHUWLUWCUUTYSUWHUWLUWCGGZYRUUQUVAUWNGU
      URUVAUWHUWLYRUWCYRUWHUWLUVAUWCYRUWHUWLUWDYRUWHUWLJZUUECVJZUWDUUCUUDCVLZUV
      RUWPUVAUWAUUFUVRUWAUVAUWPUUFUVRUWAUUECNMZUVAUWPUUFGGUVRUWAUWRUUEUUCCVMVQU
      VAUWPUWRUUFUUTUWPUWRUUFGGZYSYRUUQUWSUURYRUWPUWRUUFUUECVNZQVOKVPWHVRVSSQVT
      WAVOWBWCSWDWEWFRQVPOPORWSWEQUTWGWCUVMVFZUUCUUDNMZUVNUUDTDZUUCTDZUXAUXBWTU
      VTUVSUUDUUCTTWIWJUXBUUCUUDVBMZUXEUVNUUCUUDWKZUXFUVJUXEUXEUVLUVJUXELUUDVBM
      ZUXEUVLGUVJUXEUXGLUUCUUDWLVQUXGUXEUVBUVKUXGUXEUVBGZGUUCIUXGUXEUVAUUCIHZUU
      FUXGUXEUVAUXIUUFGZGUUTUXJYSUUQYRUXJUURUXIUVGUUFUXIUUEIUUDFZHZUVGUUFGUUCIU
      UDUEZUVGUUFUXLUXKCDUVGUXKICUUDWMZUVIWNUUEUXKCUQWOSUTUSKWPWAUXGUUCIUIZUXHU
      XGUXOUXEUVBUXGUXOUXEJZUUEUUDUUCFZVDMZUXQUUDVDMZUVBUUCUUDUVSUVTWQZUUDUUCUV
      TUVSVEZUXRUXSEZUUEUUDVDMZUVAUUFUUEUXQUUDWRZUVAUYCUUFGZUUTYSUVAUYEGZUURYRY
      SUYFGZUUQUURYRUYGYRYSUURUYFYRYSUURUYFUWFUYFYRYSUUOUWFUYFGUUPUWGUWHUWFUYFU
      WIYRYSUUPUWHUYFGZUUOUWKUWLUYHUWMUVAUWLUWHUYEUVAUWLUWHUYEGGUVAUWHUWLUVAUYE
      UVAYRUWHUWLUYFGGUUQUURYRYSXAZYRUWHUWLUYFUWOUWPUYFUWQUWPUVAUYCUUFUWPUVAUYC
      JYRUWPUWRUUFUUQUURYRYSUWPUYCXBUWPUVAUYCXCUVAUYCUWRUWPUVAUYCEUYCUUDCNMZUWR
      UVAUYCXDUUTUYJYSUYCUURYRUYJUUQUURYRUYJUUPYRUYJGUUOYRUUPUYJUUDCVHVIXEOPZXF
      UUEUUDCVMZXGPUWTXHQSQSVRRXJSWDWEWFRQVPOPORWEXIQUTXKXLWHWCXMXNXOUVJVFZUUCL
      NMZUVLLTDZUXDUYMUYNWTXPUVSLUUCTTWIWJUXGUYNUVLGZUXGUYNUVKUVAUUFUXGUYNUVKJZ
      UVAEZYRUWPUWRUUFUUQUURYRYSUYQXQUYRUWOUWPUVAUWOUYQUVAYRUWHUWLUYIUUTYSUWHUU
      RYRYSUWHGZUUQUURYRUYSUUOYRUYSGUUPYRYSUUOUWHYRYSUUOUWHUWIQVPKOPOZUUTYSUWLU
      URYRYSUWLGZUUQUURYRVUAUUPYRVUAGUUOYRYSUUPUWLYRYSUUPUWLUWMQVPXEOPOZXRXEUWQ
      SUXGUYNUVKUVAUWRUVKUXGUYNUVAUWRGZUVKUVOUXGUYNVUCGGUVQUYNUXGUVOVUCUYNUXGUV
      OVUCGZUYNUXGEZVUDUXCVUEVUEVUDGZGUVTUXCVUEUXBVUFUUCLUUDTXSUXBVUFGZUUCIUXIU
      XLUXKIHZVUGUXMUXNUXLVUHEUVEVUGUUEUXKIUKUXBVUEUVOUVEVUCUXBVUEUVOUVEVUCGZGV
      UIUVOUVAUVEUWRUUTUVEUWRGZYSUUQYRVUJUURYRUUQVUJYRUUQEZUWRUVEICNMZYRVULUUQC
      UCYAYBUUEICNXTURUPUSKUTYCWPWASYDUXBUXEUXOVUFUXFVUEUXOUXEVUDUXGUXOUXEVUDGG
      UYNUXGUXOUXEVUDUXPUXRUXSVUDUXTUYAUYBUYCVUDUYDUYCVUCUVOUYCUVAUYCUYJEZUWRUY
      CUVAVUMUVAUYJUYCUUTUYJYSUYKKYEVQUYLWHYLSXIQXEXJYMXKWHYFRVQWCWGXJYGUWTXHYH
      UXGVFZUUDLNMZUYPUYOUXCVUNVUOWTXPUVTLUUDTTWIWJUYNVUOUVLUYNVUOEUUELNMZUVLUU
      CUUDYIVUPUVKUVAUUFVUPUVKUVAJZYRUWPUWRUUFUUQUURYRYSVUPUVKYJZVUQYRUWHUWLUWP
      VURUVAVUPUWHUVKUYTVOUVAVUPUWLUVKVUBVOUWQXHVUPUVAUWRUVKUVAVUPUWRUUTVUPUWRG
      ZYSUUQYRVUSUURYRUUQVUSVUKVUPLCVBMZUWRCYKYRVUPVUTEUWRGUUQUUELCUCXSKYNUPUSK
      YOUSUWTXHQSVIXNXOXNXOXOYHSWBYPXJYQ $.
      $( [15-Apr-2011] $)
  $}

  $( Set exponentiation is an element of a transitive Tarski's class.  JFM
     CLASSES2 th. 67 (partly).  (Contributed by FL, 15-Apr-2011.) $)
  sexptrt $p |- ( ( T e. Tarski /\ Tr T /\ ( A e. T /\ B e. T ) )
    -> ( A ^m B ) e. T ) $=
    ( ctsk wcel wtr wa w3a cmap co cxp pm3.22 3anim3i cptarc cpw com23 3ad2ant1
    wi syl5com pm2.43i tskpw mapsspw elincin 3exp adantl 3ad2ant3 ex com12 3syl
    wss ) CDEZCFZACEZBCEZGZHZABIJZCEZUPUKULUNUMGZHBAKZCEZUPURRZUOUSUKULUMUNLMBA
    CNUPVAURUPVAURRZUKULUPVCRUOUKVAUPURUKVAVBUKVAGUTOZCEZUPURUTCUAUPVEURRZUOUKU
    PVFRZULUNVGUMUNUQVDUJZUPVFABCUBUKULVHVFRUOUKVEVHURUKVEVHURVDUQCUCUDPQSUEUFT
    SUGPQTUHUIT $.
    $( [15-Apr-2011] $)


  $( If an element of a Tarski's class is an ordinal number, its successor is a
     part of the class.  JFM CLASSES2 th. 6 (partly) (Contributed by FL,
     22-Feb-2011.) $)
  tarsuc1 $p |- ( ( T e. Tarski /\ Ord A /\ A e. T ) -> suc A C_ T ) $=
    ( ctsk wcel word w3a csuc csn cun tartord wss snssi 3ad2ant3 unssd syl5eqss
    df-suc ) BCDZAEZABDZFZAGAAHZIBAPTAUABABJSQUABKRABLMNO $.
    $( [22-Feb-2011] $)

  $( If an element of a Tarski's class is an ordinal number, its successor is
     an element of the class.  JFM CLASSES2 th. 6 (partly).  (Contributed by
     FL, 22-Feb-2011.) $)
  tarsuc2 $p |- ( ( T e. Tarski /\ Ord A /\ A e. T ) -> suc A e. T ) $=
    ( wcel cen wbr wn csdm com wi wa cvv syl6 com23 mpcom syl5com com12 pm2.43i
    ex syl cfn ctsk word w3a csuc wss csn cun df-suc simp3 c0 wne snnzg tsksdom
    cdom snex jctir cxp infxpg xpsneng ensymg entr unexg sylancl ensdomtr com3l
    com24 anidms com13 3expia 3adant2 omex domtri notbid notnot2 isfinite2 snfi
    unfi isfinite3 tclinf sdomdomtr exp3a adantr 3ad2ant1 3ad2ant3 sylbi syl6bi
    ne0i mpid mpan pm2.61d syl5eqbr sdomnen tarsuc1 tarax3c 3exp mp2d ) BUACZAU
    BZABCZUCZAUDZBDEFZXABUEZXABCZWTXABGEXBWTXAAAUFZUGZBGAUHWTHAUNEZXFBGEZWSWTXG
    XHIWQWRWSUIZWSXGWTXHWSXEUJUKZXGWTXHIZABULXGXJXKWTXGXJJZXHWTXLXHIZWQWSWTXMIW
    RWQWSJABGEZWTXMABUMWSXEKCZJZWTXNXMIWTWSXOXIAUOZUPXPXLXNWTXHXLXPXNXKIZXGXJXP
    XRXGXJXPUCAXEUQZXFDEZXRAXEBKURWTXNXTXHWSWTXNXTXHIIZXIWSWTYAIZWSWSJXSADEZWTY
    AAABBUSWSWTYCYAIXIWSYCWTYAWSYCAXSDEZYBXSABUTYDXTXNWTXHYDXTAXFDEZXRYDXTYEAXS
    XFVARWTYEXNXHWTXFKCZYEXNXHIZIWTWSXOYFXIXQAXEBKVBVCYFYEXFADEZYGAXFKUTYHXNXHX
    FABVDRLSVELVFLMNOVGNVHSVIPVFNOVJQPROMNWTXGFZXHIZWSWQWTYJIWRWSYIWTXHHKCZWSYI
    XKIVKYKWSJZYIAHGEZFZFZXKYLXGYNHAKBVLVMYOYMXKYMVNYMATCZXETCZXKAVOAVPYPYQJXFT
    CZXKAXEVQYRXFHGEZXKXFVRWTYSXHWTYSXHIZWSWQWTYTIWRWSBUJUKZWTYTBAWGWQWRUUAYTIW
    SWQUUAYTWQUUAJYSHBUNEZXHBVSWQYSUUBXHIIUUAWQYSUUBXHXFHBUAVTWAWBWHRWCOWDQPWES
    VCSWFWIMWDQWJWKXABWLSABWMWQWRXBXCXDIIWSWQXCXBXDWQXCXBXDXABWNWOMWCWP $.
    $( [22-Feb-2011] $)

  $( If an element of a Tarski's class is transitive and if its successor
     belongs to the class, the element belongs to the class.  (Contributed by
     FL, 20-Nov-2011.) $)
  tarsuc3 $p |- ( ( T e. Tarski /\ Tr A /\ suc A e. T ) -> A e. T ) $=
    ( ctsk wcel wtr csuc cvv wi elex sucexb sucidg suctr w3a tartrel sseld 3exp
    com4l syl com13 sylbir mpcom 3imp ) BCDZAEZAFZBDZABDZUFUDUCUGUEGDZUFUDUCUGH
    ZHZUEBIUHAGDZUFUJHZAJUKAUEDZULAGKUDUFUMUIUDUEEZUFUMUIHHALUCUNUFUMUGUCUNUFUM
    UGHUCUNUFMUEBAUEBNOPQRSRTUASUB $.
    $( [20-Nov-2011] $)

  ${
    $d A t x $.  $d t w x z $.
    $( Any set has a Tarski's class that contains it.  (Contributed by FL,
       17-Apr-2011.) $)
    settrcon $p |- ( A e. V -> E. t e. Tarski A e. t ) $=
      ( vx vz vw cv wcel ctsk wrex wceq eleq1 rexbidv wa wex cpw wss wral exbii
      mpbir cen wbr wo w3a axgroth5 3anass bicomi ancom vex eltsk anbi2i df-rex
      bitri vtoclg ) DGZAGZHZAIJZBUPHZAIJDBCUOBKUQUSAIUOBUPLMURUPIHZUQNZAOZVBUQ
      EGZPZUPQVDFGQFUPJNEUPRZVCUPUAUBVCUPHUCEUPPRZNZNZAOZVIUQVEVFUDZAODAEFUEVHV
      JAVJVHUQVEVFUFUGSTVAVHAVAUQUTNVHUTUQUHUTVGUQEFUPAUIUJUKUMSTUQAIULTUN $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d a b y $.
    $( A function that maps a set ` a ` to the smallest Tarski's class that
       contains the set. $)
    df-tskmp $a |- tarskiMap = { <. a , b >. | b = |^| { y | ( a e. y /\
      y e. Tarski ) } } $.
  $}

  ${
    $d A a y $.  $d A x y $.  $d a b y $.  $d w x y z $.
    tarval2.1 $e |- A e. _V $.
    $( Value of our tarski map.  (Contributed by FL, 30-Dec-2010.) $)
    tarval2 $p |- ( tarskiMap ` A ) = |^| { y | ( A e. y /\
      y e. Tarski ) } $=
      ( vb va vx vz vw ctarskim cfv cv wcel cab cint wceq wex ax-17 eleq1 mpbir
      wa ctsk copab df-tskmp fveq1i wne cvv anbi1d exbidv cpw wss wrex wral cen
      c0 wbr wo w3a axgroth5 3anass bicomi exbii eltsk anbi2i vtoclf abn0 intex
      vex mpbi abbid inteqd fvopab eqtri ) BIJBDKEKZAKZLZVNUALZTZAMZNZOEDUBZJBV
      NLZVPTZAMZNZBIVTAEDUCUDEDBVSWDCWCUNUEZWDUFLWEWBAPZFKZVNLZVPTZAPZWFFBWFFQC
      WGBOZWIWBAWKWHWAVPWGBVNRUGUHWJWHGKZUIZVNUJWMHKUJHVNUKTGVNULZWLVNUMUOWLVNL
      UPGVNUIULZTZTZAPZWRWHWNWOUQZAPFAGHURWQWSAWSWQWHWNWOUSUTVASWIWQAVPWPWHGHVN
      AVGVBVCVASVDWBAVESWCVFVHVMBOZVRWCWTVQWBAWTAQWTVOWAVPVMBVNRUGVIVJVKVL $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x y $.
    $( Value of our tarski map.  (Contributed by FL, 30-Dec-2010.) $)
    tarval2g $p |- ( A e. V -> ( tarskiMap ` A ) = |^| { y | ( A e. y /\
      y e. Tarski ) } ) $=
      ( vx cv ctarskim cfv wcel ctsk wa cab cint wceq fveq2 eleq1 anbi1d abbidv
      inteqd eqeq12d vex tarval2 vtoclg ) DEZFGZUCAEZHZUEIHZJZAKZLZMBFGZBUEHZUG
      JZAKZLZMDBCUCBMZUDUKUJUOUCBFNUPUIUNUPUHUMAUPUFULUGUCBUEOPQRSAUCDTUAUB $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Being a part of ` ( tarskiMap `` A ) ` .  (Contributed by FL,
       17-Apr-2011.) $)
    bpmp $p |- ( ( A e. V /\ B e. W ) ->
      ( B C_ ( tarskiMap ` A ) <-> A. x e. Tarski ( A e. x ->
        B C_ x ) ) ) $=
      ( vy wcel wa wss cv ctsk wi wral wceq wal df-ral pm2.27 albii eleq1 eleq2
      ctarskim cfv cab tarval2g adantr sseq2d ssint wb ex com13 com3r imp com12
      cint impbii sseq2 imbi12d cbvalv bitri anbi12d elab imbi1i 3bitr4i syl5bb
      vex a1i bitrd ) BDGZCEGZHZCBUAUBZICBAJZGZVLKGZHZAUCZUNZIZVMCVLIZLZAKMZVJV
      KVQCVHVKVQNVIABDUDUEUFVRCFJZIZFVPMZVJWAFCVPUGWDWBVPGZWCLZFOZVJWAWCFVPPWGW
      AUHVJBWBGZWBKGZHZWCLZFOZVNVTLZAOZWGWAWLWIWHWCLZLZFOWNWKWPFWKWPWHWIWKWCWHW
      IWKWCLWJWCQUIUJWJWPWCWHWIWPWCLWIWPWHWCWIWOQUKULUMUORWPWMFAWBVLNZWIVNWOVTW
      BVLKSWQWHVMWCVSWBVLBTWBVLCUPUQUQURUSWFWKFWEWJWCVOWJAWBFVEVLWBNVMWHVNWIVLW
      BBTVLWBKSUTVAVBRVTAKPVCVFVDVDVG $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d V y $.  $d W y $.
    $( Belonging to ` ( tarskiMap `` A ) ` .  (Contributed by FL,
       17-Apr-2011.) $)
    btmp $p |- ( ( A e. V /\ B e. W ) ->
    ( B e. ( tarskiMap ` A ) <-> A. x e. Tarski ( A e. x -> B e. x ) ) ) $=
      ( vy wcel wa cv ctsk wral wi wceq wb wal df-ral cvv eleq2 pm2.27 syl5bb
      ctarskim cfv cab tarval2g adantr eleq2d elintg adantl eleq1 anbi12d elabg
      cint vex ax-mp imbi1i albii ax-17 ex com13 com3r imp com12 impbii syl6bbr
      a1i albid imbi12d cbvralv syl6bb 3bitrd ) BDGZCEGZHZCBUAUBZGCBAIZGZVOJGZH
      ZAUCZULZGZCFIZGZFVSKZVPCVOGZLZAJKZVMVNVTCVKVNVTMVLABDUDUEUFVLWAWDNVKFCVSE
      UGUHWDWBVSGZWCLZFOZVMWGWCFVSPWJBWBGZWBJGZHZWCLZFOZVMWGWIWNFWHWMWCWBQGWHWM
      NFUMVRWMAWBQVOWBMVPWKVQWLVOWBBRVOWBJUIUJUKUNUOUPVMWOWKWCLZFJKZWGVMWOWLWPL
      ZFOWQVMWNWRFVMFUQWNWRNVMWNWRWKWLWNWCWKWLWNWCLWMWCSURUSWMWRWCWKWLWRWCLWLWR
      WKWCWLWPSUTVAVBVCVEVFWPFJPVDWPWFFAJWBVOMWKVPWCWEWBVOBRWBVOCRVGVHVITTVJ $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A y $.
    $( The set ` A ` is an element of the smallest Tarski's class that contains
       ` A ` .  CLASSES1 th. 5.  (Contributed by FL, 30-Dec-2010.) $)
    elttar $p |- ( A e. V -> A e. ( tarskiMap ` A ) ) $=
      ( vy wcel cv ctsk wa cab ctarskim cfv wi wal simpl ax-gen elintabg mpbiri
      cint tarval2g eleqtrrd ) ABDZAACEZDZUAFDZGZCHQZAIJTAUEDUDUBKZCLUFCUBUCMNU
      DCABOPCABRS $.
      $( [30-Dec-2010] $)
  $}

  $( The intersection of all Tarski's classes is the empty set.  (Contributed
     by FL, 20-Apr-2011.) $)
  inttar1 $p |- |^| Tarski = (/) $=
    ( c0 ctsk wcel cint wceq 0tsk int0el ax-mp ) ABCBDAEFBGH $.
    $( [20-Apr-2011] $)

  ${
    $d A t z $.
    $( The intersection of a collection of Tarski's classes is a Tarski's
       class.  (Contributed by FL, 17-Apr-2011.) $)
    intartar $p |- ( ( A C_ Tarski /\ A =/= (/) ) -> |^| A e. Tarski ) $=
      ( vz vt ctsk wss wa wcel cv cpw cen wbr wo cvv wi vex sylbir ex sylbi syl
      wral c0 cint elintg biimpd ax-mp dfss3 r19.26 tskpwss ralimi ssint sylibr
      wne tskpw grothpwex elint2 jca a1d com23 syl5 ralrimiv elpw r19.27av ssel
      tsken expcom syl9 adantr impcom com12 ralimia biimpri adantl wn fordisxex
      imp wrex intss1 sndw 3exp com4l mpi rexlimiv imp31 condisd orcomd syl6 wb
      mpd intex biimpi eltsk2g mpbird ) ADEZAUAULZFZAUBZDGZBHZIZWPEZWSWPGZFZBWP
      TZWRWPJKZWRWPGZLZBWPIZTZFZWOXCXHWOXBBWPXEWRCHZGZCATZWOXBWRMGZXEXLNBOZXMXE
      XLCWRAMUCUDUEWMWNXLXBNZWMXJDGZCATZWNXONCADUFXQXLWNXBXQXLWNXBNZXQXLFXPXKFZ
      CATZXRXPXKCAUGXTXBWNXTWTXAXTWSXJEZCATWTXSYACAWRXJUHUICWSAUJUKXTWSXJGZCATX
      AXSYBCAWRXJUMUICWSABUNUOUKUPUQPQURRVOUSUTWOXFBXGWRXGGZWOXFYCWRWPEZWOXFNZW
      RWPXNVAYDWRXJEZCATZYECWRAUJZYGWOYFWOFZCATZXFYGWOYJYFWOCAVBQYJWRXJJKZXKLZC
      ATZXFYIYLCAYIXJAGZYLWOYFYNYLNZWMYFYONWNWMYNXPYFYLADXJVCYFWRXJIGZXPYLNWRXJ
      XNVAXPYPYLWRXJVDVEPVFVGVHVIVJYJYGWOCATZFYMXFNZYFWOCAUGYGYRYQYGYDYRYHYMYDX
      FYMYDFZXEXDYSXLXEXDXLXEYSXEXLCWRAXNUOVKVLYMYDXLVMZXDYMYTYDXDYMYTYDXDNZYMY
      TFYKCAVPUUAYKXKCAVNYKUUACAYNWPXJEZYKUUANZXJAVQUUBXJMGZUUCCOYDUUBUUDYKXDYD
      UUBUUDYKXDNWRWPXJVRVSVTWASWBSQURWCWDWEVEPVGRWHWFRRVIUTUPWOWPMGZWQXIWGWNUU
      EWMWNUUEAWIWJVLBMWPWKSWL $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A x $.  $d T x $.
    $( The intersection of an element of a transitive Tarski's class is an
       element of the class.  (Contributed by FL, 17-Apr-2011.) $)
    intrtael $p |- ( ( T e. Tarski /\ Tr T /\ ( A e. T /\ A =/= (/) ) )
      -> |^| A e. T ) $=
      ( vx ctsk wcel wtr c0 wne wa w3a cint wss csdm wbr simp1 inttrp 3expb cvv
      wi syl6 3adant1 cv wex intss1 cdom ssdom2g trel tsksdom domsdomtr syl5com
      n0 vex ex expcom exp3a com15 ax-mp mpcom exlimiv sylbi com3l 3imp tskssel
      impcom syl3anc ) BDEZBFZABEZAGHZIZJVFAKZBLZVKBMNZVKBEVFVGVJOVGVJVLVFVGVHV
      IVLBAPQUAVFVGVJVMVJVFVGVMVIVHVFVGVMSSZVICUBZAEZCUCVHVNSZCAUKVPVQCVKVOLZVP
      VQVOAUDVOREZVRVPVQSZSCULVSVRVKVOUENZVTVKVORUFVGVPVHVFWAVMVGVPVHVFWAVMSZSZ
      VGVPVHIVOBEZWCBVOAUGVFWDWBVFWDIVOBMNZWAVMVOBUHWAWEVMVKVOBUIUMUJUNTUOUPTUQ
      URUSUTVDVAVBVKBVCVE $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d X x $.  $d t x $.
    $( A Tarski's class that contains ` X ` is a Tarski's class.  (Contributed
       by FL, 17-Apr-2011.) $)
    tmpts $p |- ( X e. A -> ( tarskiMap ` X ) e. Tarski ) $=
      ( vx vt cv ctarskim cfv ctsk wcel wceq fveq2 eleq1d cvv cab wss cin ax-mp
      wa wex mpbir vex cint tarval2g c0 wne inab abid2 ineq2 inss2 sseq1 mpbiri
      eqsstr3i wrex settrcon exancom df-rex bitr4i abn0 intartar mp2an syl6eqel
      syl vtoclg ) CEZFGZHIZBFGZHICBAVDBJVEVGHVDBFKLVDMIZVFCUAZVHVEVDDEZIZVJHIZ
      RZDNZUBZHDVDMUCVNHOVNUDUEZVOHIVNVKDNZVLDNZPZHVKVLDUFVRHJZVSHOZDHUGVTVSVQH
      PZJZWAVRHVQUHWCWAWBHOVQHUIVSWBHUJUKVBQULVPVMDSZWDVKDHUMZVHWEVIDVDMUNQWDVL
      VKRDSWEVKVLDUOVKDHUPUQTVMDURTVNUSUTVAQVC $.
      $( [17-Apr-2011] $)
  $}

  $c tar $. $( A function to study Tarski's classes. $)

  $( Extends class notation to include function ` tar ` . $)
  ctar $a class tar $.

  ${
    $d a b c x y u v $.
    $( A function to study Tarski's classes.  See ~ valdom for its domain,
       ~ vtare for its value at ` (/) ` , ~ vtarsu for its value at a
       successor, ~ vtarl for its value at a limit ordinal. $)
    df-tar $a |- tar = { <. <. a , b >. , c >. | c = ( rec
         ( { <. x , y >. | y = ( ( ( { u | E. v e. x u C_ v } u.
           { u | E. v e. x u = ~P v } ) u.
            ~P x ) i^i ( tarskiMap ` a ) ) } , { a } ) |` b ) } $.
  $}

  ${
    $d X a b c x y $.  $d Y a b c $.  $d a b c u v x y $.
    $( The ` tar ` function as a recursive function.  (Contributed by FL,
       20-Mar-2011.) $)
    valtar $p |- ( ( X e. A /\ Y e. B ) -> ( tar ` <. X , Y >. ) = ( rec
       ( { <. x , y >. | y = ( ( ( { u | E. v e. x u C_ v } u.
         { u | E. v e. x u = ~P v } ) u.
          ~P x ) i^i ( tarskiMap ` X ) ) } , { X } ) |` Y ) ) $=
      ( vc va vb wcel wa cfv cv wceq ctarskim cres cvv syl cop ctar wss cab cpw
      wrex cun cin copab csn crdg copab2 df-tar fveq1i a1i df-opr eqcomi adantr
      co elex adantl wfun con0 wfn fnfun resfunexg sylancom fveq2 ineq2d eqeq2d
      rdgfnon opabbidv rdgeq1 sneq rdgeq2 eqtrd reseq1 reseq2 cxp wrel reloprab
      cdm reldmoprab resid2 mp2an resoprab eqtr3i oprabval2g syl3anc 3eqtrd ) G
      ELZHFLZMZGHUAZUBNZWNIOBOZDOZCOZUCCAOZUFDUDWQWRUEPCWSUFDUDUGWSUEUGZJOZQNZU
      HZPZABUIZXAUJZUKZKOZRZPZJKIULZNZGHXKUSZWPWTGQNZUHZPZABUIZGUJZUKZHRZWOXLPW
      MWNUBXKABCDJKIUMUNUOXLXMPWMXMXLGHXKUPUQUOWMGSLZHSLZXTSLZXMXTPWKYAWLGEUTUR
      WLYBWKHFUTVAWKWLXSVBZYCWMXSVCVDZYDYEWMXRXQVKUOVCXSVETXSHFVFVGJKIGHSSXIXTX
      KXSXHRZSXAGPZXGXSPXIYFPYGXGXQXFUKZXSYGXEXQPXGYHPYGXDXPABYGXCXOWPYGXBXNWTX
      AGQVHVIVJVLXFXEXQVMTYGXFXRPYHXSPXAGVNXFXRXQVOTVPXGXSXHVQTXHHXSVRXKSSVSRZX
      KXASLXHSLMXJMJKIULXKVTXKWBVTYIXKPXJJKIWAXJJKIWCXKWDWEXJJKISSWFWGWHWIWJ $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d w x y z X $.
    $( The domain of the ` tar ` function is the ordinal ` Y ` .  (Contributed
       by FL, 20-Mar-2011.) $)
    valdom $p |- ( ( X e. A /\ Y e. On )
      -> dom ( tar ` <. X , Y >. ) = Y ) $=
      ( vy vw vz vx cfv cv wss wrex cab cpw wceq cun cin wcel con0 cdm wi copab
      cop ctar ctarskim csn crdg cres valtar dmres wfn rdgfnon fndm onss biimpi
      wa df-ss ineq2 eqeq1d syl5ibr syl5com adantl ax-mp syl5eq dmeq mpcom ) BC
      UBUCHZDIEIZFIZJFGIZKELVGVHMNFVIKELOVIMOBUDHPNGDUAZBUEZUFZCUGZNZBAQZCRQZUO
      ZVFSZCNZGDFEARBCUHVQVSVNVMSZCNVQVTCVLSZPZCVLCUIVLRUJZVQWBCNZTVKVJUKWCWARN
      ZVQWDRVLULVPWEWDTVOVPCRJZWEWDCUMWFWDWECRPZCNZWFWHCRUPUNWEWBWGCWARCUQURUSU
      TVAUTVBVCVNVRVTCVFVMVDURUSVE $.
      $( [20-Mar-2011] $)

    $( Value of ` tar ` at ` (/) ` .  JFM CLASSES1 th. 10.  (Contributed by FL,
       20-Mar-2011.) $)
    vtare $p |- ( ( X e. A /\ Y e. On /\ Y =/= (/) )
     -> ( ( tar ` <. X , Y >. ) ` (/) ) = { X } ) $=
      ( vy vw vz vx wcel con0 c0 wne cfv wceq cv wrex cab cpw cun syl cvv copab
      w3a cop ctar csn wss ctarskim cin crdg cres on0eln0 biimpar 3adant1 fvres
      snex a1i rdg0g 3ad2ant1 eqtrd wb wa valtar fveq1d eqeq1d 3adant3 mpbird )
      BAHZCIHZCJKZUBZJBCUCUDLZLZBUEZMZJDNENZFNZUFFGNZOEPVOVPQMFVQOEPRVQQRBUGLUH
      MGDUAZVMUIZCUJZLZVMMZVJWAJVSLZVMVJJCHZWAWCMVHVIWDVGVHWDVICUKULUMJCVSUNSVG
      VHWCVMMZVIVGVMTHZWEWFVGBUOUPVMTVRUQSURUSVGVHVNWBUTVIVGVHVAZVLWAVMWGJVKVTG
      DFEAIBCVBVCVDVEVF $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d A u v x y $.  $d X u v x y $.  $d Y u v x y $.
    $( The parts and the powersets of the elements of ` tar ( A ) ` are
       elements of ` tar ( suc A ) ` .  As well as the parts of ` tar ( A ) `
       when they are elements of the smallest Tarski's class of which ` X ` is
       an element.  JFM CLASSES1 th. 11.  (Contributed by FL, 20-Mar-2011.) $)
    vtarsu $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) -> ( ( tar `
    <. X , Y >. ) ` suc A ) = ( ( ( { u | E. v e. ( ( tar ` <. X , Y
    >. ) ` A ) u C_ v } u. { u | E. v e. ( ( tar ` <. X , Y >. ) ` A
    ) u = ~P v } ) u. ~P ( ( tar ` <. X , Y >. ) ` A ) ) i^i (
    tarskiMap ` X ) ) ) $=
      ( vy vx wcel con0 cfv cv wrex cab cpw wceq cun ctarskim cin cvv csuc ctar
      w3a cop wss copab csn crdg valtar 3adant3 fveq1d fvres 3ad2ant3 ordsuccl2
      cres onelon syldan 3adant1 rdgsuc syl eqcomd fveq2d fvex inex2 a1i abbidv
      wa rexeq uneq12d pweq ineq1d fvopabg sylancr eqtrd 3eqtrd ) EDIZFJIZCUAZF
      IZUCZVREFUDUBKZKVRGLBLZALZUEZAHLZMZBNZWBWCOPZAWEMZBNZQZWEOZQZERKZSZPHGUFZ
      EUGZUHZFUOZKZVRWRKZWDACWAKZMZBNZWHAXBMZBNZQZXBOZQZWNSZVTVRWAWSVPVQWAWSPVS
      HGABDJEFUIZUJUKVSVPWTXAPVQVRFWRULUMVTXACWRKZWPKZCWSKZWPKZXJVTCJIZXAXMPVQV
      SXPVPVQVSCFIZXPCFUNZFCUPUQURWQCWPUSUTVTXLXNWPVTXQXLXNPVQVSXQVPXRURXQXNXLC
      FWRULVAUTVBVTXOXBWPKZXJVTXNXBWPVTCWSWAVPVQWSWAPVSVPVQVGWAWSXKVAUJUKVBVTXB
      TIXJTIZXSXJPCWAVCXTVTWNXIERVCVDVEHGXBWOXJTTWEXBPZWMXIWNYAWKXGWLXHYAWGXDWJ
      XFYAWFXCBWDAWEXBVHVFYAWIXEBWHAWEXBVHVFVIWEXBVJVIVKVLVMVNVOVO $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d u v x y z X $.
    $( The value of ` tar ` at a limit ordinal.  JFM CLASSES1 th. 12.
       (Contributed by FL, 20-Mar-2011.) $)
    vtarl $p |- ( ( X e. B /\ Y e. On /\ ( A e. Y /\ Lim A ) )
      -> ( ( tar ` <. X , Y >. ) ` A ) = U. ( ( tar ` <. X , Y >. ) " A ) ) $=
      ( vy vu vv vx wcel con0 wa cfv cv wrex cab cpw wceq cun cima cuni w3a cop
      wlim ctar wss ctarskim cin copab csn crdg cres valtar fveq1d simp3l fvres
      3adant3 syl rdglim 3ad2ant3 word 3ad2ant2 imresord syl2anc eqcomd imaeq1d
      eloni eqtr3d unieqd eqtrd 3eqtrd ) CBIZDJIZADIZAUCZKZUAZACDUBUDLZLZAEMFMZ
      GMZUEGHMZNFOVSVTPQGWANFORWAPRCUFLUGQHEUHZCUIZUJZDUKZLZAWDLZVQASZTZVKVLVRW
      FQVOVKVLKAVQWEHEGFBJCDULZUMUPVPVMWFWGQVKVLVMVNUNZADWDUOUQVPWGWDASZTZWIVOV
      KWGWMQVLWCADWBURUSVPWLWHVPWEASZWLWHVPDUTZVMWNWLQVLVKWOVODVFVAWKADWDVBVCVP
      WEVQAVPVQWEVKVLVQWEQVOWJUPVDVEVGVHVIVJ $.
      $( [20-Mar-2011] $)
  $}


  ${
    $d A b c $.  $d B b $.  $d X b c u v w x y z $.  $d Y b c u v $.
    $( The sequence ` tar ` has its values in a Tarski's class.  (Contributed
       by FL, 20-Mar-2011.) $)
    tartarmap $p |- ( ( X e. A /\ Y e. On /\ suc B e. Y ) ->
      ( ( tar ` <. X , Y >. ) ` B ) C_ ( tarskiMap ` X ) ) $=
      ( vc csuc con0 wcel w3a cfv wss wi cv c0 wceq sseq1d cpw syl wral wa ctar
      vb vu vv vy vw vz vx ctarskim onelon 3adant1 sucelon suceq eleq1d 3anbi3d
      cop fveq2 imbi12d csn ne0i vtare syl3an3 elttar 3ad2ant1 wb snssg eqsstrd
      wne mpbid wrex cab cun cin inss2 simp1 simp2 word eloni ordtr simp3 trsuc
      3ad2ant2 syl2anc 3jca 3ad2ant3 vtarsu mpbiri 3exp wlim cima simp31 simp32
      wtr cuni ordsuccl2 adantl simpl jca 3adant2 vtarl syl3anc r19.27av ordtr1
      ordon ax-mp sylbir ancoms ordsucelsuc simpl1 simpl2 a1i com12 com13 3imp1
      syl6 expcom syl6bi pm2.43a pm5.31 com3r imp impcom ralimiaa r19.26 3simpa
      ex ralimi wfun cdm copab crdg cres valtar wfn rdgfnon fnfun funeq 3adant3
      funres ordsuccl3 valdom sseqtr4d funimass4 fvex ralbii syl6bb uniss unipw
      elpw syl6sseq syl6bir com23 sylbi pm2.43d 3imp tfinds mpcom ) BFZGHZCAHZD
      GHZUURDHZIZBCDUPUAJZJZCUIJZKZUVAUVBUUSUUTDUURUJUKUUSBGHUVCUVGLZBULUUTUVAU
      BMZFZDHZIZUVIUVDJZUVFKZLUUTUVANFZDHZIZNUVDJZUVFKZLUUTUVAEMZFZDHZIZUVTUVDJ
      ZUVFKZLZUUTUVAUWAFZDHZIZUWAUVDJZUVFKZLUVHUBEBUVINOZUVLUVQUVNUVSUWLUVKUVPU
      UTUVAUWLUVJUVODUVINUMUNUOUWLUVMUVRUVFUVINUVDUQPURUVIUVTOZUVLUWCUVNUWEUWMU
      VKUWBUUTUVAUWMUVJUWADUVIUVTUMUNUOUWMUVMUWDUVFUVIUVTUVDUQPURUVIUWAOZUVLUWI
      UVNUWKUWNUVKUWHUUTUVAUWNUVJUWGDUVIUWAUMUNUOUWNUVMUWJUVFUVIUWAUVDUQPURUVIB
      OZUVLUVCUVNUVGUWOUVKUVBUUTUVAUWOUVJUURDUVIBUMUNUOUWOUVMUVEUVFUVIBUVDUQPUR
      UVQUVRCUSZUVFUVPUUTUVADNVHUVRUWPODUVOUTACDVAVBUVQCUVFHZUWPUVFKZUUTUVAUWQU
      VPCAVCVDUUTUVAUWQUWRVEUVPCUVFAVFVDVIVGUVTGHZUWFUWIUWKUWSUWFUWIIZUWKUCMZUD
      MZKUDUWDVJUCVKUXAUXBQOUDUWDVJUCVKVLUWDQVLZUVFVMZUVFKZUXCUVFVNUWTUWCUWKUXE
      VEUWIUWSUWCUWFUWIUUTUVAUWBUUTUVAUWHVOUUTUVAUWHVPUWIDWMZUWHUWBUVAUUTUXFUWH
      UVADVQZUXFDVRZDVSRWBUUTUVAUWHVTDUWAWAWCWDWEUWCUWJUXDUVFUDUCUVTACDWFPRWGWH
      UVIWIZUWFEUVISZUVLUVNUXIUXJUVLIZUVMUVDUVIWJZWNZUVFUXKUUTUVAUVIDHZUXITZUVM
      UXMOUXIUXJUUTUVAUVKWKUXIUXJUUTUVAUVKWLUXIUVLUXOUXJUXIUVLTUXNUXIUVLUXNUXIU
      VAUVKUXNUUTUVIDWOUKWPUXIUVLWQWRWSUVIACDWTXAUXIUXJUVLUXMUVFKZUXJUVLUXPLZLU
      XIUXJUVLUXPUXJUVLUXQUXJUVLTUWFUVLTZEUVISZUXQUWFUVLEUVIXBUXSUWEUWCTZEUVISZ
      UXQUXRUXTEUVIUXRUVTUVIHZUXTUWFUVLUYBUXTLUVLUYBUWFUXTUVLUYBUWCUWFUXTLUYBUV
      LUWCUVLUVIVQZUYBUVLUWCLZLUVAUVKUYCUUTUVKUVAUYCUVKUVATZUVJGHZUYCGVQUYEUYFL
      XDUVJDGXCXEUYFUVIGHUYCUVIULUVIVRXFRXGUKUYCUYBUWAUVJHZUYDUVTUVIXHUVLUYGUWC
      UVLUYGTUUTUVAUWBUUTUVAUVKUYGXIUUTUVAUVKUYGXJUUTUVAUVKUYGUWBUUTUVAUXGUVKUY
      GUWBLLUVAUXGLUUTUXHXKUYGUVKUXGUWBUYGUVKUXGUWBLUXGUYGUVKTUWBUWAUVJDXCXLYFX
      MXOXNWDXPXQRXRUWFUWCUXTUWCUWFUWCUXTLUWCUWEUWCXSYFXRXOXTYAYBYCUYAUWEEUVISZ
      UWCEUVISZTUXQUWEUWCEUVIYDUYIUYHUXQUYIUUTUVATZEUVISZUYHUXQLUWCUYJEUVIUUTUV
      AUWBYEYGUYKUVLUYHUXPUYKUVLUVDYHZUVIUVDYIZKZTZUYHUXPLUYKUVLUYOUYKUVLTUYLUY
      NUVLUYLUYKUUTUVAUYLUVKUYJUVDUEMUFMZUGMZKUGUHMZVJUFVKUYPUYQQOUGUYRVJUFVKVL
      UYRQVLUVFVMOUHUEYJZUWPYKZDYLZOZUYLUHUEUGUFAGCDYMVUBUYLVUAYHZUYTYHZVUCUYTG
      YNVUDUWPUYSYOGUYTYPXEDUYTYSXEUVDVUAYQWGRYRWPUVLUYNUYKUVLUVIDUYMUVAUVKUVID
      KUUTUVIDYTUKUUTUVAUYMDOUVKACDUUAYRUUBWPWRYFUYOUYHUXLUVFQZKZUXPUYOVUFUWDVU
      EHZEUVISUYHEUVIVUEUVDUUCVUGUWEEUVIUWDUVFUVTUVDUUDUUIUUEUUFVUFUXMVUEWNUVFU
      XLVUEUUGUVFUUHUUJUUKXOUULRYBUUMRRYFUUNXKUUOVGWHUUPXFUUQ $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d A t $.  $d X t $.
    $( If ` A ` belongs to the smallest Tarski's class that contains ` X ` so
       does ` ~P A ` .  CLASSES1 th. 7.  (Contributed by FL, 17-Apr-2011.) $)
    pwtsm $p |- ( ( X e. B /\ A e. ( tarskiMap ` X ) )
      -> ~P A e. ( tarskiMap ` X ) ) $=
      ( vt wcel ctarskim cfv wa cpw cv wi ctsk wral biimpd ex pm2.43d imp tskpw
      btmp cvv imim2d ralimia syl wb pwexg sylan2 mpbird ) CBEZACFGZEZHZAIZUIEZ
      CDJZEZULUNEZKZDLMZUKUOAUNEZKZDLMZURUHUJVAUHUJVAUHUJUJVAKUKUJVADCABUISNOPQ
      UTUQDLUNLEZUSUPUOVBUSUPAUNROUAUBUCUJUHULTEUMURUDAUIUEDCULBTSUFUG $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A t $.  $d X t $.
    $( If ` A ` belongs to the smallest Tarski's class that contains ` X ` so
       does the subsets of ` A ` .  CLASSES1. th. 6.  (Contributed by FL,
       17-Apr-2011.) $)
    subtsm $p |- ( ( X e. B /\ A e. ( tarskiMap ` X ) )
      -> ~P A C_ ( tarskiMap ` X ) ) $=
      ( vt wcel ctarskim cfv wa cpw wss cv wi ctsk wral btmp tskpwss ex ralimia
      imim2d cvv syl6bi pm2.43d imp wb pwexg bpmp sylan2 mpbird ) CBEZACFGZEZHZ
      AIZUJJZCDKZEZUMUOJZLZDMNZUIUKUSUIUKUSUIUKUKUSLULUKUPAUOEZLZDMNUSDCABUJOVA
      URDMUOMEZUTUQUPVBUTUQAUOPQSRUAQUBUCUKUIUMTEUNUSUDAUJUEDCUMBTUFUGUH $.
      $( [17-Apr-2011] $)
  $}

  $( If ` A ` is a subset of the smallest Tarski's class that contains ` X `
     then it is equipotent to this class or it belongs to it.  CLASSES1 th. 8.
     (Contributed by FL, 17-Apr-2011.) $)
  subtareqbe $p |- ( ( X e. B /\ A e. ~P ( tarskiMap ` X ) ) ->
      ( A ~~ ( tarskiMap ` X ) \/ A e. ( tarskiMap ` X ) ) ) $=
    ( wcel ctarskim cfv ctsk cpw cen wbr wo tmpts tsken sylan ) CBDCEFZGDAOHDAO
    IJAODKBCLAOMN $.
    $( [17-Apr-2011] $)

  $c tr $.

  $( $)

  $( Extend class notation to include the function whose value is the
     transitive closure of its operand. $)
  ctr $a class tr $.

  ${
    $d x y a z w $.
    $( The transitive closure of a set. $)
    df-trcls $a |- tr = { <. x , y >. | y = U_ a e. om ( ( rec ( { <. z , w >.
      | w = ( z u. U. z ) } , x ) |` om ) ` a ) } $.
  $}

  ${
    $d A a x y $.  $d a w x y z $.
    $( The transitive closure of a set A. (Contributed by FL, 17-Apr-2011.) $)
    trclval $p |- ( A e. B
    -> ( tr ` A ) = U_ a e. om
     ( ( rec ( { <. z , w >. | w = ( z u. U. z ) } , A ) |` om ) ` a ) ) $=
      ( vx vy wcel cvv com cv cuni cun wceq copab crdg cres cfv ciun ctr iunexg
      elex wral omex fvex a1i mp2an rdgeq2 reseq1 syl fveq1d adantr iuneq2dv wa
      rgen df-trcls wrel relopab resid ax-mp resopab 3eqtr2i fvopab4g sylancl )
      CDHCIHEJEKZBKAKZVFLMNABOZCPZJQZRZSZIHZCTRVKNCDUBJIHVJIHZEJUCVLUDVMEJVMVEJ
      HZVEVIUEUFUOEJVJIIUAUGFGCEJVEVGFKZPZJQZRZSZVKIITVOCNZEJVRVJVTVRVJNVNVTVEV
      QVIVTVPVHNVQVINVOCVGUHVPVHJUIUJUKULUMTGKVSNZFGOZWBIQZVOIHWAUNFGOFGABEUPWB
      UQWCWBNWAFGURWBUSUTWAFGIVAVBVCVD $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A u v $.  $d A v z $.  $d B v $.  $d C u v $.  $d C v z $.  $d X u v $.
    $d X v z $.  $d Y u v $.  $d Y v z $.
    $( C belongs to the value of ` tar ` at a successor of ` A ` iff it is a
       part of ` tar ` at ` A ` , the powerset of an element or a part of an
       element of ` tar ` at ` A ` .  CLASSES1 th. 13 (Contributed by FL,
       13-Apr-2011.) $)
    vtarsuelt $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) -> ( C e. ( ( tar
    ` <. X , Y >. ) ` suc A ) <-> ( ( C C_ ( ( tar ` <. X , Y >. ) ` A ) /\
    C e. ( tarskiMap ` X ) ) \/ E. z e. ( ( tar ` <. X , Y >. ) ` A )
    ( C C_ z \/ C = ~P z ) ) ) ) $=
      ( vu vv wcel cfv cv wss wrex cpw wceq wa wo wb a1i wi w3a cop cab cun cin
      con0 csuc ctar ctarskim vtarsu eleq2d elin elun anbi1d andir orbi1d orcom
      elpwg sseq1 rexbidv elabg eqeq1 orbi12d r19.43 bicomi tartarmap sseld imp
      pm5.32rd subtsm vex elpw2 ssel com12 sylbir syl5com ex com23 a1d 3ad2ant1
      pwtsm eleq1 imbi2d syl5ibrcom adantr jaoi com13 mpcom pm4.71 sylib bicomd
      rexbidva r19.41v sseq2 pweq eqeq2d cbvrexv 3bitr3g 3bitrd syl5bb bitrd )
      ECIZFUFIZBUGZFIZUAZDXDEFUBUHJZJZIDGKZHKZLZHBXGJZMZGUCZXIXJNZOZHXLMZGUCZUD
      ZXLNZUDZEUIJZUEZIZDXLLZDYBIZPZDAKZLZDYHNZOZQZAXLMZQZXFXHYCDHGBCEFUJUKXFYD
      DYAIZYFPZDXSIZDXTIZQZYFPZYNYDYPRXFDYAYBULSXFYOYSYFYOYSRXFDXSXTUMSUNXFYTYQ
      YFPZYRYFPZQZDXNIZDXRIZQZYFPZUUBQZYNYTUUCRXFYQYRYFUOSXFUUAUUGUUBXFYQUUFYFY
      QUUFRXFDXNXRUMSUNUPUUHUUBUUGQXFYNUUGUUBUQXFUUBYGUUGYMXFYFYRYEYFYRYERTXFDX
      LYBURSVIXFUUGDXJLZHXLMZDXOOZHXLMZQZYFPUUIUUKQZHXLMZYFPZYMXFYFUUFUUMYFUUFU
      UMRTXFYFUUDUUJUUEUULXMUUJGDYBXIDOZXKUUIHXLXIDXJUSUTVAXQUULGDYBUUQXPUUKHXL
      XIDXOVBUTVAVCSVIXFUUMUUOYFUUMUUORXFUUOUUMUUIUUKHXLVDVESUNXFUUNYFPZHXLMUUO
      UUPYMXFUURUUNHXLXFXJXLIZPZUUNUURUUTUUNYFTZUUNUURRXJYBIZUUTUVAXFUUSUVBXFXL
      YBXJCBEFVFVGVHUUNUUTUVBYFUUIUUTUVBYFTZTUUKUUTUUIUVCXFUUSUUIUVCTZXBXCUUSUV
      DTXEXBUVDUUSXBUVBUUIYFXBUVBUUIYFTXBUVBPXOYBLZUUIYFXJCEVJUUIDXOIZUVEYFTDXJ
      HVKVLUVEUVFYFXOYBDVMVNVOVPVQVRVSVTVHVNUUTUUKUVCXFUUKUVCTZUUSXBXCUVGXEXBUV
      CUUKUVBXOYBIZTXBUVBUVHXJCEWAVQUUKYFUVHUVBDXOYBWBWCWDVTWEVNWFWGWHUUNYFWIWJ
      WKWLUUNYFHXLWMUUNYLHAXLXJYHOZUUIYIUUKYKXJYHDWNUVIXOYJDXJYHWOWPVCWQWRWSVCW
      TWSWSXA $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A z $.  $d C z $.  $d X z $.  $d Y z $.  $d Z z $.
    $( If ` C ` is a part of an element of our tar function at ` A ` then ` C `
       is an element or tar at ` suc A ` .  CLASSES1 th. 14 (Contributed by FL,
       13-Apr-2011.) $)
    partarelt1 $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( ( C C_ Z /\ Z e. ( ( tar ` <. X , Y >. ) ` A ) ) ->
          C e. ( ( tar ` <. X , Y >. ) ` suc A ) ) ) $=
      ( vz wss cop ctar cfv wcel wa con0 csuc w3a cpw wceq wo wi orc wrex sseq2
      cv pweq eqeq2d orbi12d rcla4ev ctarskim olc vtarsuelt syl5ibrcom ex mpan9
      syl com12 ) CFHZFADEIJKZKZLZMDBLENLAOZELPZCVAURKLZUQUQCFQZRZSZUTVBVCTZUQV
      EUAUTVFVGUTVFMCGUDZHZCVHQZRZSZGUSUBZVGVLVFGFUSVHFRZVIUQVKVEVHFCUCVNVJVDCV
      HFUEUFUGUHVMVCVBCUSHCDUIKLMZVMSVMVOUJGABCDEUKULUOUMUNUP $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A u v $.  $d X u v $.  $d Y u v $.  $d Z u v $.
    $( If ` Z ` is an element of our tar function at ` A ` then ` ~P Z ` is an
       element or tar at ` suc A ` .  CLASSES1 th. 15 (Contributed by FL,
       13-Apr-2011.) $)
    partarelt2 $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( Z e. ( ( tar ` <. X , Y >. ) ` A ) ->
          ~P Z e. ( ( tar ` <. X , Y >. ) ` suc A ) ) ) $=
      ( vu vv wcel con0 csuc w3a cfv cpw cv wrex cab wceq cun syl2anc cvv simpr
      cop ctar wa wss ctarskim eqidd pweq eqeq2d rcla4ev wb pwexg eqeq1 rexbidv
      cin elabg syl adantl mpbird elun2 elun1 3syl simpl1 tartarmap sseld pwtsm
      imp elin sylanbrc vtarsu adantr eleqtrrd ex ) CBHZDIHZAJZDHZKZEACDUBUCLZL
      ZHZEMZVPVSLZHVRWAUDZWBFNZGNZUEGVTOFPZWEWFMZQZGVTOZFPZRZVTMZRZCUFLZUOZWCWD
      WBWNHZWBWOHZWBWPHWDWBWKHZWBWLHWQWDWSWBWHQZGVTOZWDWAWBWBQZXAVRWAUAWDWBUGWT
      XBGEVTWFEQWHWBWBWFEUHUIUJSWAWSXAUKZVRWAWBTHXCEVTULWJXAFWBTWEWBQWIWTGVTWEW
      BWHUMUNUPUQURUSWBWKWGUTWBWLWMVAVBWDVNEWOHZWRVNVOVQWAVCVRWAXDVRVTWOEBACDVD
      VEVGEBCVFSWBWNWOVHVIVRWCWPQWAGFABCDVJVKVLVM $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d X x $.  $d Y x $.
    $( All the element of ` tar ` at ` A ` are elements of ` tar ` at
       ` suc A ` .  CLASSES1 th. 18 (Contributed by FL, 13-Apr-2011.) $)
    tareltsuc $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( ( tar ` <. X , Y >. ) ` A ) C_
         ( ( tar ` <. X , Y >. ) ` suc A ) ) $=
      ( vx wcel con0 csuc w3a cop ctar cfv cv wss ssid partarelt1 mpani ssrdv )
      CBFDGFAHZDFIZEACDJKLZLZSUALZTEMZUDNUDUBFUDUCFUDOABUDCDUDPQR $.
      $( [13-Apr-2011] $)
  $}

  $( An element of the intersection of a Tarski's class with the class of the
     ordinal numbers is a part of the intersection.  (Contributed by FL,
     20-Apr-2011.) $)
  eltintpar $p |- ( T e. Tarski -> ( A e. ( On i^i T )
      -> A C_ ( On i^i T ) ) ) $=
    ( ctsk wcel con0 cin wss wa elin onss ad2antrr word wtr eloni ordtr tartrel
    wi 3exp com3l ex 3syl imp31 jca sylbi impcom ssin sylib ) BCDZAEBFZDZAUIGZU
    HUJHAEGZABGZHZUKUJUHUNUJAEDZABDZHZUHUNQAEBIUQUHUNUQUHHULUMUOULUPUHAJKUOUPUH
    UMUOALAMZUPUHUMQQANAOUHURUPUMUHURUPUMABPRSUAUBUCTUDUEAEBUFUGT $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The intersection of a Tarski's class with the class of the ordinal
       numbers is an ordinal number.  (Contributed by FL, 20-Apr-2011.) $)
    inttaror $p |- ( T e. Tarski -> ( On i^i T ) e. On ) $=
      ( vx ctsk wcel con0 cin cvv cv wa wral incom inex1g syl5eqelr inss1 sseli
      wss adantl wi elin ex word wtr eloni onss w3a tartrel ssin biimpi syl5com
      ordtr 3exp com14 syl com12 3syl pm2.43i imp sylbi impcom ralrimiv syl2anc
      jca celsor ) ACDZEAFZGDBHZEDZVFVEPZIZBVEJVEEDVDVEAEFGAEKAECLMVDVIBVEVDVFV
      EDZVIVDVJIVGVHVJVGVDVEEVFEANOQVJVDVHVJVGVFADZIVDVHRZVFEASVGVKVLVGVKVLRZVG
      VFUAVFUBZVGVMRVFUCVFUJVGVNVMVGVFEPZVNVMRVFUDVDVNVKVOVHVDVNVKVOVHRVDVNVKUE
      VFAPZVOVHVFAUFVOVPVHVOVPIVHVFEAUGUHTUIUKULUMUNUOUPUQURUSVBTUTBVEGVCVA $.
      $( [20-Apr-2011] $)
  $}

  $( The intersection of a Tarski's class and the ordinal numbers is equipotent
     to the Tarski's class.  JFM CLASSES2. .  (Contributed by FL,
     20-Apr-2011.) $)
  inttarcar $p |- ( T e. Tarski -> ( On i^i T ) ~~ T ) $=
    ( ctsk wcel con0 cin wss cen wbr inss2 wo wa tarax3a wi ax-1 inttaror elirr
    elin pm2.21i sylbir ex syl adantr com12 jaoi mpcom mpan2 ) ABCZDAEZAFZUHAGH
    ZDAIUJUHACZJUGUIKZUJUHALUJULUJMUKUJULNULUKUJUGUKUJMZUIUGUHDCZUMAOUNUKUJUNUK
    KUHUHCZUJUHDAQUOUJUHPRSTUAUBUCUDUEUF $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The cardinal of the intersection of a Tarski's class with the class of
       the ordinal numbers.  (Contributed by FL, 20-Apr-2011.) $)
    carinttar $p |- ( T e. Tarski
      -> ( card ` ( On i^i T ) ) = ( On i^i T ) ) $=
      ( vx ctsk wcel con0 cin cv csdm wbr wa cen wn cvv wi syl imp expcom mpcom
      wral sylanbrc ccrd cfv wceq inttaror cdom eltintpar incom inex1g syl5eqel
      ssdom2g syld inttarcar elin tsksdom sdomnen entr con3d com3r adantl sylbi
      wss a1i com13 brsdom ex ralrimiv iscard ) ACDZEAFZEDBGZVIHIZBVISVIUAUBVIU
      CAUDVHVKBVIVHVJVIDZVKVHVLJVJVIUEIZVJVIKIZLZVKVHVLVMVHVLVJVIVAZVMVJAUFVHVI
      MDVPVMNVHVIAEFMEAUGAECUHUIVJVIMUJOUKPVHVLVOVIAKIZVHVLVONAULVLVHVQVOVLVJED
      ZVJADZJVHVQVONZNZVJEAUMVSWAVRVHVSVTVJAHIZVHVSJZVTVJAUNWBVJAKIZLZWCVTNVJAU
      OWCVQWEVOVQWEVONNWCVQVNWDVNVQWDVJVIAUPQUQVBURORQUSUTVCRPVJVIVDTVEVFBVIVGT
      $.
      $( [20-Apr-2011] $)
  $}

  $( The cardinal of a Tarski's class equals the intersection of the Tarski's
     class with the class of the ordinal numbers.  CLASSES2 th. 10.
     (Contributed by FL, 20-Apr-2011.) $)
  carinttar2 $p |- ( T e. Tarski
      -> ( card ` T ) = ( On i^i T ) ) $=
    ( ctsk wcel ccrd cfv con0 cin wceq cen wbr inttarcar ensymg mpd wb inttaror
    carden mpdan mpbird carinttar eqtrd ) ABCZADEZFAGZDEZUCUAUBUDHZAUCIJZUAUCAI
    JUFAKUCABLMUAUCFCUEUFNAOAUCBFPQRAST $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The cardinal of an element of a Tarski's class belongs to the Tarski's
       class. th. 12 CLASSES2 (Contributed by FL, 20-Apr-2011.) $)
    cardtar $p |- ( ( T e. Tarski /\ A e. T ) -> ( card ` A ) e. T ) $=
      ( ctsk wcel wa ccrd cfv con0 cin carinttar2 adantr tarax3d3 eleq2 simprbi
      wceq elin syl6bi sylc ) BCDZABDZEBFGZHBIZOZAFGZUADZUDBDZSUCTBJKABLUCUEUDU
      BDZUFUAUBUDMUGUDHDUFUDHBPNQR $.
      $( [20-Apr-2011] $)

    $( The cardinal of a Tarski's class is a limit ordinal.  CLASSES2 th. 21.
       (Contributed by FL, 20-Apr-2011.) $)
    cartarlim $p |- ( ( T e. Tarski /\ T =/= (/) ) -> Lim ( card ` T ) ) $=
      ( vx ctsk wcel c0 wne wa ccrd wlim con0 cin word adantr cvv wb syl sylibr
      elin wi sylbi cfv cv csuc wral inttaror incom inex1g syl5eqel elong mpbid
      w3a tsk0 0elon jctil suceloni ad2antrr vex elon 3exp com3l imp31 sylanbrc
      tarsuc2 ex com12 ralrimiv 3jca dflim4 wceq carinttar2 limeq mpbird ) ACDZ
      AEFZGZAHUAZIZJAKZIZVOVRLZEVRDZBUBZUCZVRDZBVRUDZUKVSVOVTWAWEVOVRJDZVTVMWFV
      NAUEMVOVRNDZWFVTOVMWGVNVMVRAJKNJAUFAJCUGUHMVRNUIPUJVOEJDZEADZGWAVOWIWHAUL
      UMUNEJARQVOWDBVRWBVRDZVOWDWJWBJDZWBADZGZVOWDSWBJARWMVOWDWMVOGWCJDZWCADZWD
      WKWNWLVOWBUOUPWKWLVOWOWKWBLZWLVOWOSSWBBUQURVOWPWLWOVMWPWLWOSSVNVMWPWLWOWB
      AVCUSMUTTVAWCJARVBVDTVEVFVGBVRVHQVOVPVRVIZVQVSOVMWQVNAVJMVPVRVKPVL $.
      $( [20-Apr-2011] $)
  $}

  ${
    $( An element of the cardinal of the Tarski's class ` T ` is an element of
       ` T ` . th. 14 CLASSES2.  (Contributed by FL, 20-Nov-2011.) $)
    elcarelcl $p |- ( ( T e. Tarski /\ A e. ( card ` T ) )
    -> A e. T ) $=
      ( ctsk wcel ccrd cfv con0 cin wceq wi carinttar2 eleq2 simprbi syl6bi syl
      elin imp ) BCDZABEFZDZABDZRSGBHZIZTUAJBKUCTAUBDZUASUBALUDAGDUAAGBPMNOQ $.
      $( [20-Nov-2011] $)
  $}

  ${
    $d A f x $.  $d T f x $.
    $( Consider functions whose domain ` A ` is an element of a transitive
       Tarski's class ` T ` and whose range is ` T ` , then they are elements
       of ` T ` .  CLASSES2 th. 23.  (Contributed by FL, 26-Sep-2011.) $)
    fnctartar $p |- ( ( T e. Tarski /\ Tr T /\ A e. T ) -> ( T ^m A ) C_ T ) $=
      ( vf vx ctsk wcel w3a cv wi wal wss wa csdm wbr cfv imp com12 3adant2 cen
      wceq wtr cmap co wf elmapg simpr1 wel cxp fssxp ssel c1st c2nd cop simp31
      elxp6 trel exp3a com23 3adant1 adantr simp1r tskop syl3anc imbi2d syl5ibr
      3exp eleq1 sylbi syl6com com4l mpcom alrimiv dfss2 wfn wfun cdm ffn fnfun
      sylibr fndm jca vex fundmen breq1 tsksdom cvv sdomen1 biimpd mpan syl5com
      syl6bi mpan9 3syl 3jca ex tskssel syl6 pm2.43a ) BEFZBUAZABFZGZCHZBAUBUCZ
      FZXCBFZIZCJXDBKXBXGCXEXBXFWSXAXEXBXFIZIWTWSXALZXEABXCUDZXHBAXCEBUEXJXBWSX
      CBKZXCBMNZGZXFXJXBXMXJXBLZWSXKXLXJWSWTXAUFXNDCUGZDHZBFZIZDJXKXNXRDXJXBXRX
      CABUHZKZXJXBXRIABXCUIXOXTXJXBXQXTXOXPXSFZXJXBXQIZIZXCXSXPUJYAXPXPUKOZXPUL
      OZUMZTZYDAFZYEBFZLZLYCXPABUOYGYJYCYJYCYGXJXBYFBFZIZIYJXJXBYKYJXJXBGWSYDBF
      ZYIYKYJXJWSWTXAUNYJXBYMXJYJXBYMYHXBYMIYIXBYHYMWTXAYHYMIZWSWTXAYNWTYHXAYMW
      TYHXAYMBYDAUPUQURPUSQUTPRYHYIXJXBVAYDYEBVBVCVFYGYBYLXJYGXQYKXBXPYFBVGVDVD
      VEPVHVIVJVKPVLDXCBVMVSXJXBXLXJXCAVNZXCVOZXCVPZATZLXBXLIZABXCVQYOYPYRAXCVR
      AXCVTWAYPYQXCSNZYRYSXCCWBZWCYRYTAXCSNZYSYQAXCSWDXBUUBXLWSXAUUBXLIWTXIABMN
      ZUUBXLABWEXCWFFZUUBUUCXLIUUAUUDUUBLUUCXLAXCBWFWGWHWIWJRQWKWLWMPWNWOXCBWPW
      QWKRWRVLCXDBVMVS $.
      $( [26-Sep-2011] $)
  $}

  ${
    $d A f x $.  $d T f x $.
    $( Consider functions whose domain ` A ` is an element and a part of a
       Tarski's class ` T ` and whose range is ` T ` , then they are elements
       of ` T ` .  CLASSES2 th. 23.  (Contributed by FL, 27-Sep-2011.) $)
    fnctartar2 $p |- ( ( T e. Tarski /\ A C_ T /\ A e. T ) -> ( T ^m A ) C_ T )
        $=
      ( vf vx ctsk wcel wss w3a cv wi wal csdm wbr 3adant2 wa ssel cfv wceq imp
      cen cmap co wf wb elmapg simpr1 wel fssxp c1st c2nd elxp6 simp31 3ad2ant2
      cxp cop com12 adantr simp1r tskop syl3anc 3exp eleq1 imbi2d syl5ibr sylbi
      syl6com com4l mpcom alrimiv dfss2 wfn wfun cdm ffn jca fnfun fndm anim12i
      sylibr vex fundmen breq1 tsksdom sdomen1 biimpd mpan syl5com syl6bi mpan9
      cvv 3syl 3jca ex pm2.43a tskssel syl6 ) BEFZABGZABFZHZCIZBAUAUBZFZXABFZJZ
      CKXBBGWTXECWTXCWQXABGZXABLMZHZXDXCWTXHWTXCABXAUCZWTXHJWQWSXCXIUDWRBAXAEBU
      ENXIWTXHXIWTOZWQXFXGXIWQWRWSUFXJDCUGZDIZBFZJZDKXFXJXNDXIWTXNXAABUNZGZXIWT
      XNJABXAUHXKXPXIWTXMXPXKXLXOFZXIWTXMJZJZXAXOXLPXQXLXLUIQZXLUJQZUOZRZXTAFZY
      ABFZOZOXSXLABUKYCYFXSYFXSYCXIWTYBBFZJZJYFXIWTYGYFXIWTHWQXTBFZYEYGYFXIWQWR
      WSULYFWTYIXIYFWTYIYDWTYIJYEWTYDYIWRWQYDYIJWSABXTPUMUPUQSNYDYEXIWTURXTYABU
      SUTVAYCXRYHXIYCXMYGWTXLYBBVBVCVCVDSVEVFVGVHSVIDXABVJVSXIWTXGXIXAAVKZYJOXA
      VLZXAVMZARZOWTXGJZXIYJYJABXAVNZYOVOYJYKYJYMAXAVPAXAVQVRYKYLXATMZYMYNXACVT
      ZWAYMYPAXATMZYNYLAXATWBWTYRXGWQWSYRXGJWRWQWSOABLMZYRXGABWCXAWJFZYRYSXGJYQ
      YTYROYSXGAXABWJWDWEWFWGNUPWHWIWKSWLWMWHWNXABWOWPVICXBBVJVS $.
      $( [27-Sep-2011] $)
  $}

  $( If the cardinal of ` A ` of a part of ` T ` is less than ` T ` . a
     function from ` A ` to ` T ` is a part of ` T ` .  CLASSES2 th. 23.
     (Contributed by FL, 20-Nov-2011.) $)
  fnctartar3 $p |- ( ( T e. Tarski /\ A C_ T /\
      ( card ` A ) e. ( card ` T ) ) -> ( T ^m A ) C_ T ) $=
    ( ctsk wcel wss ccrd cfv cmap co csdm wbr wa wb ssexg ancoms simpl cardsdom
    cvv syl2anc syld3an3 biimp3a tskssel fnctartar2 ) BCDZABEZAFGBFGDZABDZBAHIB
    EUDUEUFABJKZUGUDUEUFUHUDUELARDZUDUFUHMUEUDUIABCNOUDUEPABRCQSUAABUBTABUCT $.
    $( [20-Nov-2011] $)

