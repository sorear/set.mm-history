$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Tarski's classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c tarskiMap $.
  $( A function whose value is the smallest Tarski's classe
     containing a particular set. $)

  $( Extend class definition to include the map whose value is the smallest
     Tarski's class. $)
  ctarskim $a class tarskiMap $.

  $( The parts of an element of a Tarski's class are Tarski's classes. $)
  elincin $p |- ( ( T e. Tarski /\ A e. T /\ B C_ A ) -> B e. T ) $=
    ( ctsk wcel wss w3a cvv ssexg ancoms 3adant1 cpw wi elpwg biimprcd
    3ad2ant3 wa tskpwss ssel2 a1d ex syl 3adant3 syld mp2d ) CDEZACEZBAFZGZBHEZ
    UJBCEZUGUHUJUFUHUGUJBACIJKZULUIUJBALZEZUJUKMZUHUFUJUNMUGUJUNUHBAHNOPUFUGUNU
    OMZUHUFUGQUMCFZUPACRUQUNUOUQUNQUKUJUMCBSTUAUBUCUDUE $.
    $( [30-Dec-2010] $)

  $( The intersection of two elements of a Tarski's class belongs to the
     class. $)
  inacint $p |- ( ( T e. Tarski /\ A e. T /\ B e. T ) -> ( A i^i B ) e. T ) $=
    ( ctsk wcel w3a cpw cin wss tskpwss 3adant3 ssid elpwg mpbiri 3ad2ant2
    inpws1 syl sseldd ) CDEZACEZBCEZFZAGZCABHZSTUCCIUAACJKUBAUCEZUDUCETSUEUATUE
    AAIALAACMNOABAPQR $.
    $( [30-Dec-2010] $)

  $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class is either
     equipotent to the class or an element of the class. $)
  tarax3a $p |- ( ( T e. Tarski /\ A C_ T ) ->
      ( A ~~ T \/ A e. T ) ) $=
    ( ctsk wcel wss wa cen wbr wo wi cvv ssexg cpw elpwg tsken expcom syl6bir
    com13 imp syl5com ancoms pm2.43i ) BCDZABEZFZABGHABDIZUDUCUEUFJUDUCFAKDZUEU
    FABCLUCUDUGUFJUGUDUCUFUGUDABMDZUCUFJABKNUCUHUFABOPQRSTUAUB $.
    $( [17-Apr-2011] $)

  $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class non
     equipotent to the class is an element of the class. $)
  tarax3b $p |- ( ( T e. Tarski /\ A e. ~P T /\ -. A ~~ T ) ->
       A e. T ) $=
    ( ctsk wcel cpw cen wbr wn wa tsken ord 3impia ) BCDZABEDZABFGZHABDZMNIOPAB
    JKL $.
    $( [22-Feb-2011] $)

  $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class non
     equipotent to the class is an element of the class. $)
  tarax3c $p |- ( ( T e. Tarski /\ A C_ T /\ -. A ~~ T ) ->
       A e. T ) $=
    ( ctsk wcel wss cen wbr wn w3a cpw cvv wb ssexg ancoms 3adant3 elpwg
    bicomd 3anbi2d syl tarax3b syl6bi pm2.43i ) BCDZABEZABFGHZIZABDZUFUFUCABJDZ
    UEIZUGUFAKDZUFUILUCUDUJUEUDUCUJABCMNOUJUDUHUCUEUJUHUDABKPQRSABTUAUB $.
    $( [22-Feb-2011] $)

  $( The cardinal of an element of a Tarski's class belongs to the cardinal of
     the Tarski's class.  JFM CLASSES2 th. 1 $)
  tarax3d3 $p |- ( ( T e. Tarski /\ A e. T )
    -> ( card ` A ) e. ( card ` T ) ) $=
    ( ctsk wcel wa ccrd cfv csdm wbr tsksdom wb cardsdom ancoms mpbird ) BCDZAB
    DZEAFGBFGDZABHIZABJPOQRKABBCLMN $.
    $( [20-Apr-2011] $)


  $( The cardinal of an element of a Tarski's class is strictly less than the
     cardinal of the Tarski's class.  JFM CLASSES2 th. 1 $)
  tarax3d4 $p |- ( ( T e. Tarski /\ A e. T )
    -> ( card ` A ) ~< ( card ` T ) ) $=
    ( ctsk wcel wa ccrd cfv csdm wbr tarax3d3 wceq cardidm a1i eleq12d mpbird
    cvv wb fvex pm3.2i cardsdom syl mpbid ) BCDABDEZAFGZFGZBFGZFGZDZUDUFHIZUCUH
    UDUFDABJUCUEUDUGUFUEUDKUCALMUGUFKUCBLMNOUCUDPDZUFPDZEZUHUIQULUCUJUKAFRBFRSM
    UDUFPPTUAUB $.
    $( [20-Apr-2011] $)

  $( An element of a transitive Tarski's class is a part of the class.  JFM
     CLASSES2 th. 48 $)
  elcartr $p |- ( ( T e. Tarski /\ Tr T /\ A e. T ) -> A C_ T ) $=
    ( wtr wcel wss ctsk trss imp 3adant1 ) BCZABDZABEZBFDJKLBAGHI $.
    $( [30-Dec-2010] $)

  $( A transitive element of a Tarski's class is a part of the class.  JFM
     CLASSES2 th. 8 $)
  tartrel $p |- ( ( T e. Tarski /\ Tr A /\ A e. T ) -> A C_ T ) $=
    ( ctsk wcel wtr wss wi cpw dftr4 wa tskpwss sstr2 syl5com ex com3r sylbi
    com12 3imp ) BCDZAEZABDZABFZTSUAUBGZTAAHZFZSUCGAISUAUEUBSUAUEUBGSUAJUDBFUEU
    BABKAUDBLMNOPQR $.
    $( [22-Feb-2011] $)

  $( If an element of a Tarski's class is an ordinal number, it is a part of
     the class.  JFM CLASSES2 th. 6 (partly) $)
  tartord $p |- ( ( T e. Tarski /\ Ord A /\ A e. T ) -> A C_ T ) $=
    ( ctsk wcel word wss wi wtr cep wwe wa df-ord tartrel 3exp com12 adantr
    sylbi 3imp ) BCDZAEZABDZABFZTSUAUBGZTAHZAIJZKSUCGZALUDUFUESUDUCSUDUAUBABMNO
    PQOR $.
    $( [22-Feb-2011] $)

  ${
    $d T t $.  $d t x $.
    $( A non empty Tarski class is infinite. $)
    tclinf $p |- ( ( T e. Tarski /\ T =/= (/) ) -> om ~<_ T ) $=
      ( vt vx ctsk wcel c0 wne com cdom wbr cv wa wi wceq eleq1 neeq1 anbi12d
      breq2 imbi12d cuni wss simpr wal cpw visset pwid tskpw elunii sylancr ex
      adantr 19.21aiv dfss2 sylibr dominf syl11anc vtoclg anabsi5 ) ADEZAFGZHAI
      JZBKZDEZVBFGZLZHVBIJZMUSUTLZVAMBADVBANZVEVGVFVAVHVCUSVDUTVBADOVBAFPQVBAHI
      RSVEVDVBVBTZUAZVFVCVDUBVECKZVBEZVKVIEZMZCUCVJVEVNCVCVNVDVCVLVMVCVLLVKVKUD
      ZEVOVBEVMVKCUEUFVKVBUGVKVOVBUHUIUJUKULCVBVIUMUNVBBUEUOUPUQUR $.
      $( [22-Feb-2011] $)
  $}

  ${
    $d A a b $.  $d B b $.  $d T a b $.
    $( The cross product of two elements of a transitive Tarski's class is an
       element of the class.  JFM CLASSES2 th. 67 (partly). $)
    cptarc $p |- ( ( T e. Tarski /\ Tr T /\ ( A e. T /\ B e. T ) )
    -> ( A X. B ) e. T ) $=
      ( va vb ctsk wcel wtr wa cxp cv wi wceq xpeq1 eleq1d imbi2d xpeq2 c0 wne
      ne0i w3a xp0 eqtr mpan2 tsk0 ancoms eleq1 syl5ibrcom 3adant2 adantr
      com12 3syl com cdom wbr wn df-ne cen visset infxpabs csdm tsksdom expcom
      imp 3adant1 wss elcartr tskxpss ensdomtr ex tskssel 3exp 3ad2ant3 com3r
      syl6 com24 com4l syl com14 com4r anabsi5 com13 3adant3l syl5com 3adant3r
      pm2.43i mpd sylbir cvv wb domtri2 mp2an sdomdom domtr xp0r syl5eqel
      syl5ibr a1i12 xpcomen entr simpl3 simp2l3 simp1 simpr adantl ad2antrr
      syl11anc syl111anc com3l sylancr pm2.61ine a1dd sylc sylbi pm2.61i omex
      simprl3 3jca sdomdomtr 0sdomg biimpar breq1 a1i sylancl anim2i a1d syl5
      ax-mp 3imp1 3exp1 cptwff simp3l3 tclinf mpan2d impcom vtocl2ga 3imp ) CFG
      ZCHZACGBCGIZABJZCGZYTYRYSUUBYRYSDKZEKZJZCGZLZLZYRYSAUUDJZCGZLZLYRYSUUBLZL
      DEABCCUUCAMZUUGUUKYRUUMUUFUUJYSUUMUUEUUICUUCAUUDNOPPUUDBMZUUKUULYRUUNUUJU
      UBYSUUNUUIUUACUUDBAQOPPUUCCGZUUDCGZUUHUUOCRSZUUOUUPIZUUHLCUUCTUUQUURYRYSU
      UFUUDRMZUUQUURYRUAZYSIZUUFLZUUSUUEUUCRJZMZUUERMZUVBUUDRUUCQUVDUVCRMUVEUUC
      UBUUEUVCRUCUDUVAUVEUUFUUTUVEUUFLZYSUUQYRUVFUURUUQYRIZUUFUVERCGZYRUUQUVHCU
      EUFZUUERCUGUHUIUJUKULUMUUCUNUOZUUSUPZUVBLZUUDUUCUNUOZUVJUVLLZUVKUVJUVMUVB
      UVKUUDRSZUVJUVMUVBLZLUUDRUQZUVJUVOUVPUVJUVOUVMUVBUVJUVOUVMUAUUEUUCURUOZUV
      AUUFUUCUUDDUSZEUSZUTUVAUUCCVAUOZUVRUUFLZUUTUWAYSUURYRUWAUUQUURYRUWAUUOYRU
      WALUUPYRUUOUWAUUCCVBVCUJVDVEUJUVAUWAUWBLZUUTYSUVAUWCLZUURYRYSUWDLZUUQUURY
      RUWEYRYSUURUWDYRYSUURUWDYRYSUURUAZUWDYRYSUUOUWFUWDLUUPYRYSUUOUAZUUCCVFZUW
      FUWDUUCCVGZYRYSUUPUWHUWDLZUUOYRYSUUPUAZUUDCVFZUWJUUDCVGZUVAUWHUWLUWCUUTYS
      UWHUWLUWCLLZYRUUQUVAUWNLUURUVAUWHUWLYRUWCYRUWHUWLUVAUWCYRUWHUWLUWDYRUWHUW
      LUAZUUECVFZUWDUUCUUDCVHZUVRUWPUVAUWAUUFUVRUWAUVAUWPUUFUVRUWAUUECVAUOZUVAU
      WPUUFLLUVRUWAUWRUUEUUCCVIVJUVAUWPUWRUUFUUTUWPUWRUUFLLZYSYRUUQUWSUURYRUWPU
      WRUUFUUECVKZVLVMUJVNVOVPVQVRVLVSVTVMWAWBVRWCWDWEWFVLVNVDVEVDWFWGWDVLUKWHW
      BUVMUPZUUCUUDVAUOZUVNUUDWIGZUUCWIGZUXAUXBWJUVTUVSUUDUUCWIWIWKWLUXBUUCUUDU
      NUOZUXEUVNUUCUUDWMZUXFUVJUXEUXEUVLUVJUXEUMUUDUNUOZUXEUVLLUVJUXEUXGUMUUCUU
      DWNVJUXGUXEUVBUVKUXGUXEUVBLZLUUCRUXGUXEUVAUUCRMZUUFUXGUXEUVAUXIUUFLZLUUTU
      XJYSUUQYRUXJUURUXIUVGUUFUXIUUERUUDJZMZUVGUUFLUUCRUUDNZUVGUUFUXLUXKCGUVGUX
      KRCUUDWOZUVIWPUUEUXKCUGWQVRUKUIUJWRVTUXGUUCRSZUXHUXGUXOUXEUVBUXGUXOUXEUAZ
      UUEUUDUUCJZURUOZUXQUUDURUOZUVBUUCUUDUVSUVTWSZUUDUUCUVTUVSUTZUXRUXSIZUUEUU
      DURUOZUVAUUFUUEUXQUUDWTZUVAUYCUUFLZUUTYSUVAUYELZUURYRYSUYFLZUUQUURYRUYGYR
      YSUURUYFYRYSUURUYFUWFUYFYRYSUUOUWFUYFLUUPUWGUWHUWFUYFUWIYRYSUUPUWHUYFLZUU
      OUWKUWLUYHUWMUVAUWLUWHUYEUVAUWLUWHUYELLUVAUWHUWLUVAUYEUVAYRUWHUWLUYFLLUUQ
      UURYRYSXAZYRUWHUWLUYFUWOUWPUYFUWQUWPUVAUYCUUFUWPUVAUYCUAYRUWPUWRUUFUUQUUR
      YRYSUWPUYCXBUWPUVAUYCXCUVAUYCUWRUWPUVAUYCIUYCUUDCVAUOZUWRUVAUYCXDUUTUYJYS
      UYCUURYRUYJUUQUURYRUYJUUPYRUYJLUUOYRUUPUYJUUDCVBVCXEVDVEZXFUUEUUDCVIZXGVE
      UWTXHVLVRVLVRVPWFXIVRWCWDWEWFVLVNVDVEVDWFWDXJVLUKXKXLVOWBXMXNXOUVJUPZUUCU
      MVAUOZUVLUMWIGZUXDUYMUYNWJXPUVSUMUUCWIWIWKWLUXGUYNUVLLZUXGUYNUVKUVAUUFUXG
      UYNUVKUAZUVAIZYRUWPUWRUUFUUQUURYRYSUYQXQUYRUWOUWPUVAUWOUYQUVAYRUWHUWLUYIU
      UTYSUWHUURYRYSUWHLZUUQUURYRUYSUUOYRUYSLUUPYRYSUUOUWHYRYSUUOUWHUWIVLVNUJVD
      VEVDZUUTYSUWLUURYRYSUWLLZUUQUURYRVUAUUPYRVUALUUOYRYSUUPUWLYRYSUUPUWLUWMVL
      VNXEVDVEVDZXRXEUWQVRUXGUYNUVKUVAUWRUVKUXGUYNUVAUWRLZUVKUVOUXGUYNVUCLLUVQU
      YNUXGUVOVUCUYNUXGUVOVUCLZUYNUXGIZVUDUXCVUEVUEVUDLZLUVTUXCVUEUXBVUFUUCUMUU
      DWIXSUXBVUFLZUUCRUXIUXLUXKRMZVUGUXMUXNUXLVUHIUVEVUGUUEUXKRUCUXBVUEUVOUVEV
      UCUXBVUEUVOUVEVUCLZLVUIUVOUVAUVEUWRUUTUVEUWRLZYSUUQYRVUJUURYRUUQVUJYRUUQI
      ZUWRUVERCVAUOZYRVULUUQCFXTYAUUERCVAYBUHUFUIUJUKYCWRVTVRYDUXBUXEUXOVUFUXFV
      UEUXOUXEVUDUXGUXOUXEVUDLLUYNUXGUXOUXEVUDUXPUXRUXSVUDUXTUYAUYBUYCVUDUYDUYC
      VUCUVOUYCUVAUYCUYJIZUWRUYCUVAVUMUVAUYJUYCUUTUYJYSUYKUJYEVJUYLVOYFVRXJVLXE
      XIYGXKVOYHWFVJWBWHXIYIUWTXHYJUXGUPZUUDUMVAUOZUYPUYOUXCVUNVUOWJXPUVTUMUUDW
      IWIWKWLUYNVUOUVLUYNVUOIUUEUMVAUOZUVLUUCUUDYKVUPUVKUVAUUFVUPUVKUVAUAZYRUWP
      UWRUUFUUQUURYRYSVUPUVKYLZVUQYRUWHUWLUWPVURUVAVUPUWHUVKUYTVMUVAVUPUWLUVKVU
      BVMUWQXHVUPUVAUWRUVKUVAVUPUWRUUTVUPUWRLZYSUUQYRVUSUURYRUUQVUSVUKVUPUMCUNU
      OZUWRCYMYRVUPVUTIUWRLUUQUUEUMCFXSUJYNUFUIUJYOUIUWTXHVLVRVCXNXOXNXOXOYJVRW
      AYPXIYQ $.
      $( [15-Apr-2011] $)
  $}

  $( Set exponentiation is an element of a transitive Tarski's class.  JFM
     CLASSES2 th. 67 (partly). $)
  sexptrt $p |- ( ( T e. Tarski /\ Tr T /\ ( A e. T /\ B e. T ) )
    -> ( A ^m B ) e. T ) $=
    ( ctsk wcel wtr wa w3a cmap co cxp wi pm3.22 3anim3i cptarc cpw tskpw wss
    mapsspw elincin 3exp com23 3ad2ant1 syl5com adantl 3ad2ant3 pm2.43i ex
    com12 3syl ) CDEZCFZACEZBCEZGZHZABIJZCEZUPUKULUNUMGZHBAKZCEZUPURLZUOUSUKULU
    MUNMNBACOUPVAURUPVAURLZUKULUPVCLUOUKVAUPURUKVAVBUKVAGUTPZCEZUPURUTCQUPVEURL
    ZUOUKUPVFLZULUNVGUMUNUQVDRZUPVFABCSUKULVHVFLUOUKVEVHURUKVEVHURVDUQCTUAUBUCU
    DUEUFUGUDUHUBUCUGUIUJUG $.
    $( [15-Apr-2011] $)


  $( If an element of a Tarski's class is an ordinal number, its successor is a
     part of the class.  JFM CLASSES2 th. 6 (partly) $)
  tarsuc1 $p |- ( ( T e. Tarski /\ Ord A /\ A e. T ) -> suc A C_ T ) $=
    ( ctsk wcel word w3a csuc csn cun df-suc wss wa tartord snssi 3ad2ant3 jca
    unss sylib syl5eqss ) BCDZAEZABDZFZAGAAHZIZBAJUCABKZUDBKZLUEBKUCUFUGABMUBTU
    GUAABNOPAUDBQRS $.
    $( [22-Feb-2011] $)

  $( If an element of a Tarski's class is an ordinal number, its successor is
     an element of the class.  JFM CLASSES2 th. 6 (partly). $)
  tarsuc2 $p |- ( ( T e. Tarski /\ Ord A /\ A e. T ) -> suc A e. T ) $=
    ( ctsk wcel word w3a csuc cen wbr wn wss csdm csn cun df-suc com cdom wi
    simp3 c0 wne snnzg wa tsksdom cvv snex jctir cxp infxpg xpsneng ensymg
    entr ex unexg sylancl ensdomtr syl6 syl com3l com24 com23 mpcom syl5com
    anidms com13 3expia com12 3adant2 pm2.43i omex domtri notbid notnot2 cfn
    isfinite2 snfi unfi isfinite ne0i tclinf sdomdomtr exp3a adantr mpid
    3ad2ant1 3ad2ant3 sylbir syl6bi mpan pm2.61d syl5eqbr sdomnen tarsuc1
    tarax3c 3exp mp2d ) BCDZAEZABDZFZAGZBHIJZXABKZXABDZWTXABLIXBWTXAAAMZNZBLAOW
    TPAQIZXFBLIZWSWTXGXHRWQWRWSSZWSXGWTXHWSXETUAZXGWTXHRZABUBXGXJXKWTXGXJUCZXHW
    TXLXHRZWQWSWTXMRWRWQWSUCABLIZWTXMABUDWSXEUEDZUCZWTXNXMRWTWSXOXIAUFZUGXPXLXN
    WTXHXLXPXNXKRZXGXJXPXRXGXJXPFAXEUHZXFHIZXRAXEBUEUIWTXNXTXHWSWTXNXTXHRRZXIWS
    WTYARZWSWSUCXSAHIZWTYAAABBUJWSWTYCYARXIWSYCWTYAWSYCAXSHIZYBXSABUKYDXTXNWTXH
    YDXTAXFHIZXRYDXTYEAXSXFULUMWTYEXNXHWTXFUEDZYEXNXHRZRWTWSXOYFXIXQAXEBUEUNUOY
    FYEXFAHIZYGAXFUEUKYHXNXHXFABUPUMUQURUSUQUTUQVAVBVCVDVBVEURVFVGUTVBVCVHVIVGU
    MVCVAVBWTXGJZXHRZWSWQWTYJRWRWSYIWTXHPUEDZWSYIXKRVJYKWSUCZYIAPLIZJZJZXKYLXGY
    NPAUEBVKVLYOYMXKYMVMYMAVNDZXEVNDZXKAVOAVPYPYQUCXFVNDZXKAXEVQYRXFPLIZXKXFVRW
    TYSXHWTYSXHRZWSWQWTYTRWRWSBTUAZWTYTBAVSWQWRUUAYTRWSWQUUAYTWQUUAUCYSPBQIZXHB
    VTWQYSUUBXHRRUUAWQYSUUBXHXFPBCWAWBWCWDUMWEVCWFVIVGWGURUOURWHWIVAWFVIWJWKXAB
    WLURABWMWQWRXBXCXDRRWSWQXCXBXDWQXCXBXDXABWNWOVAWEWP $.
    $( [22-Feb-2011] $)

  $( If an element of a Tarski's class is transitive and if its successor
     belongs to the class, the element belongs to the class. $)
  tarsuc3 $p |- ( ( T e. Tarski /\ Tr A /\ suc A e. T ) -> A e. T ) $=
    ( ctsk wcel wtr csuc cvv wi elisset sucexb sucidg suctr w3a tartrel sseld
    3exp com4l syl com13 sylbir mpcom 3imp ) BCDZAEZAFZBDZABDZUFUDUCUGUEGDZUFUD
    UCUGHZHZUEBIUHAGDZUFUJHZAJUKAUEDZULAGKUDUFUMUIUDUEEZUFUMUIHHALUCUNUFUMUGUCU
    NUFUMUGHUCUNUFMUEBAUEBNOPQRSRTUASUB $.
    $( [20-Nov-2011] $)

  ${
    $d A t x $.  $d t w x z $.
    $( Any set has a Tarski's class that contains it. $)
    settrcon $p |- ( A e. V -> E. t e. Tarski A e. t ) $=
      ( vx vz vw cv wcel ctsk wrex wceq eleq1 rexbidv wa wex cpw wss wral cen
      wbr wo w3a axgroth5 3anass bicomi exbii mpbir ancom visset eltsk anbi2i
      bitri df-rex vtoclg ) DGZAGZHZAIJZBUPHZAIJDBCUOBKUQUSAIUOBUPLMURUPIHZUQNZ
      AOZVBUQEGZPZUPQVDFGQFUPJNEUPRZVCUPSTVCUPHUAEUPPRZNZNZAOZVIUQVEVFUBZAODAEF
      UCVHVJAVJVHUQVEVFUDUEUFUGVAVHAVAUQUTNVHUTUQUHUTVGUQEFUPAUIUJUKULUFUGUQAIU
      MUGUN $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d a b y $.
    $( A function that maps a set ` a ` to the smallest Tarski's class that
       contains the set. $)
    df-tskmp $a |- tarskiMap = { <. a , b >. | b = |^| { y | ( a e. y /\
      y e. Tarski ) } } $.
  $}

  ${
    $d A a y $.  $d A x y $.  $d a b y $.  $d w x y z $.
    tarval2.1 $e |- A e. _V $.
    $( Value of our tarski map. $)
    tarval2 $p |- ( tarskiMap ` A ) = |^| { y | ( A e. y /\
      y e. Tarski ) } $=
      ( vb va vx vz vw ctarskim cfv cv wcel ctsk wa cab cint wceq copab
      df-tskmp fveq1i c0 wne cvv wex ax-17 eleq1 anbi1d exbidv cpw wss wrex
      wral cen wbr wo w3a axgroth5 3anass bicomi exbii mpbir visset eltsk
      anbi2i vtoclf abn0 intex mpbi abbid inteqd fvopab eqtri ) BIJBDKEKZAKZLZV
      NMLZNZAOZPZQEDRZJBVNLZVPNZAOZPZBIVTAEDSTEDBVSWDCWCUAUBZWDUCLWEWBAUDZFKZVN
      LZVPNZAUDZWFFBWFFUECWGBQZWIWBAWKWHWAVPWGBVNUFUGUHWJWHGKZUIZVNUJWMHKUJHVNU
      KNGVNULZWLVNUMUNWLVNLUOGVNUIULZNZNZAUDZWRWHWNWOUPZAUDFAGHUQWQWSAWSWQWHWNW
      OURUSUTVAWIWQAVPWPWHGHVNAVBVCVDUTVAVEWBAVFVAWCVGVHVMBQZVRWCWTVQWBAWTAUEWT
      VOWAVPVMBVNUFUGVIVJVKVL $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x y $.
    $( Value of our tarski map. $)
    tarval2g $p |- ( A e. V -> ( tarskiMap ` A ) = |^| { y | ( A e. y /\
      y e. Tarski ) } ) $=
      ( vx cv ctarskim cfv wcel ctsk wa cab cint wceq fveq2 eleq1 anbi1d
      abbidv inteqd eqeq12d visset tarval2 vtoclg ) DEZFGZUCAEZHZUEIHZJZAKZLZMB
      FGZBUEHZUGJZAKZLZMDBCUCBMZUDUKUJUOUCBFNUPUIUNUPUHUMAUPUFULUGUCBUEOPQRSAUC
      DTUAUB $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Being a part of ` ( tarskiMap `` A ) ` . $)
    bpmp $p |- ( ( A e. V /\ B e. W ) ->
      ( B C_ ( tarskiMap ` A ) <-> A. x e. Tarski ( A e. x ->
        B C_ x ) ) ) $=
      ( vy wcel wa ctarskim cfv wss cv ctsk cab cint wi wral wceq tarval2g
      adantr sseq2d ssint wal df-ral wb pm2.27 ex com13 com3r imp com12 impbii
      albii eleq1 eleq2 sseq2 imbi12d cbvalv bitri visset anbi12d elab imbi1i
      3bitr4i a1i syl5bb bitrd ) BDGZCEGZHZCBIJZKCBALZGZVLMGZHZANZOZKZVMCVLKZPZ
      AMQZVJVKVQCVHVKVQRVIABDSTUAVRCFLZKZFVPQZVJWAFCVPUBWDWBVPGZWCPZFUCZVJWAWCF
      VPUDWGWAUEVJBWBGZWBMGZHZWCPZFUCZVNVTPZAUCZWGWAWLWIWHWCPZPZFUCWNWKWPFWKWPW
      HWIWKWCWHWIWKWCPWJWCUFUGUHWJWPWCWHWIWPWCPWIWPWHWCWIWOUFUIUJUKULUMWPWMFAWB
      VLRZWIVNWOVTWBVLMUNWQWHVMWCVSWBVLBUOWBVLCUPUQUQURUSWFWKFWEWJWCVOWJAWBFUTV
      LWBRVMWHVNWIVLWBBUOVLWBMUNVAVBVCUMVTAMUDVDVEVFVFVG $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d V y $.  $d W y $.
    $( Belonging to ` ( tarskiMap `` A ) ` . $)
    btmp $p |- ( ( A e. V /\ B e. W ) ->
    ( B e. ( tarskiMap ` A ) <-> A. x e. Tarski ( A e. x -> B e. x ) ) ) $=
      ( vy wcel wa ctarskim cfv cv ctsk cab cint wral wi wceq tarval2g adantr
      eleq2d wb elintg adantl wal df-ral cvv visset eleq2 eleq1 anbi12d elabg
      ax-mp imbi1i albii ax-17 pm2.27 ex com13 com3r imp com12 impbii a1i
      albid syl6bbr imbi12d cbvralv syl6bb syl5bb 3bitrd ) BDGZCEGZHZCBIJZGCBAK
      ZGZVOLGZHZAMZNZGZCFKZGZFVSOZVPCVOGZPZALOZVMVNVTCVKVNVTQVLABDRSTVLWAWDUAVK
      FCVSEUBUCWDWBVSGZWCPZFUDZVMWGWCFVSUEWJBWBGZWBLGZHZWCPZFUDZVMWGWIWNFWHWMWC
      WBUFGWHWMUAFUGVRWMAWBUFVOWBQVPWKVQWLVOWBBUHVOWBLUIUJUKULUMUNVMWOWKWCPZFLO
      ZWGVMWOWLWPPZFUDWQVMWNWRFVMFUOWNWRUAVMWNWRWKWLWNWCWKWLWNWCPWMWCUPUQURWMWR
      WCWKWLWRWCPWLWRWKWCWLWPUPUSUTVAVBVCVDWPFLUEVEWPWFFALWBVOQWKVPWCWEWBVOBUHW
      BVOCUHVFVGVHVIVIVJ $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A y $.
    $( The set ` A ` is an element of the smallest Tarski's class that contains
       ` A ` .  CLASSES1 th. 5. $)
    elttar $p |- ( A e. V -> A e. ( tarskiMap ` A ) ) $=
      ( vy wcel cv ctsk wa cab cint ctarskim cfv wi wal simpl ax-gen elintabg
      mpbiri tarval2g eleqtrrd ) ABDZAACEZDZUAFDZGZCHIZAJKTAUEDUDUBLZCMUFCUBUCN
      OUDCABPQCABRS $.
      $( [30-Dec-2010] $)
  $}

  $( The intersection of all Tarski's classes is the empty set. $)
  inttar1 $p |- |^| Tarski = (/) $=
    ( c0 ctsk wcel cint wceq 0tsk int0el ax-mp ) ABCBDAEFBGH $.
    $( [20-Apr-2011] $)

  ${
    $d A t z $.
    $( The intersection of a collection of Tarski's classes is a Tarski's
       class. $)
    intartar $p |- ( ( A C_ Tarski /\ A =/= (/) ) -> |^| A e. Tarski ) $=
      ( vz vt ctsk wss c0 wne wa cint wcel cv cpw wral cen wbr wo cvv wi
      visset elintg biimpd ax-mp dfss3 r19.26 tskpwss ralimi ssint sylibr
      tskpw grothpwex elint2 jca a1d sylbir ex com23 sylbi imp syl5 r19.21aiv
      elpw r19.27av ssel tsken expcom syl9 adantr impcom com12 ralimia biimpri
      adantl wn wrex fordisxex intss1 sndw 3exp com4l mpi syl r19.23aiv imp31
      condisd orcomd mpd syl6 wb intex biimpi eltsk2g mpbird ) ADEZAFGZHZAIZDJZ
      BKZLZWPEZWSWPJZHZBWPMZWRWPNOZWRWPJZPZBWPLZMZHZWOXCXHWOXBBWPXEWRCKZJZCAMZW
      OXBWRQJZXEXLRBSZXMXEXLCWRAQTUAUBWMWNXLXBRZWMXJDJZCAMZWNXORCADUCXQXLWNXBXQ
      XLWNXBRZXQXLHXPXKHZCAMZXRXPXKCAUDXTXBWNXTWTXAXTWSXJEZCAMWTXSYACAWRXJUEUFC
      WSAUGUHXTWSXJJZCAMXAXSYBCAWRXJUIUFCWSABUJUKUHULUMUNUOUPUQURUSUTWOXFBXGWRX
      GJZWOXFYCWRWPEZWOXFRZWRWPXNVAYDWRXJEZCAMZYECWRAUGZYGWOYFWOHZCAMZXFYGWOYJY
      FWOCAVBUOYJWRXJNOZXKPZCAMZXFYIYLCAYIXJAJZYLWOYFYNYLRZWMYFYORWNWMYNXPYFYLA
      DXJVCYFWRXJLJZXPYLRWRXJXNVAXPYPYLWRXJVDVEUNVFVGVHVIVJYJYGWOCAMZHYMXFRZYFW
      OCAUDYGYRYQYGYDYRYHYMYDXFYMYDHZXEXDYSXLXEXDXLXEYSXEXLCWRAXNUKVKVLYMYDXLVM
      ZXDYMYTYDXDYMYTYDXDRZYMYTHYKCAVNUUAYKXKCAVOYKUUACAYNWPXJEZYKUUARZXJAVPUUB
      XJQJZUUCCSYDUUBUUDYKXDYDUUBUUDYKXDRWRWPXJVQVRVSVTWAWBWAUOUPWCWDWEVEUNVGUQ
      WFWGUQUQVIUTULWOWPQJZWQXIWHWNUUEWMWNUUEAWIWJVLBQWPWKWAWL $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A x $.  $d T x $.
    $( The intersection of an element of a transitive Tarski's class is an
       element of the class. $)
    intrtael $p |- ( ( T e. Tarski /\ Tr T /\ ( A e. T /\ A =/= (/) ) )
      -> |^| A e. T ) $=
      ( vx ctsk wcel wtr c0 wne wa w3a cint wss csdm wbr simp1 inttrp 3expb
      3adant1 wi cv wex n0 intss1 cvv visset cdom ssdom2g trel tsksdom
      domsdomtr ex syl5com expcom syl6 exp3a com15 ax-mp mpcom 19.23aiv sylbi
      impcom com3l 3imp tskssel syl111anc ) BDEZBFZABEZAGHZIZJVFAKZBLZVKBMNZVKB
      EVFVGVJOVGVJVLVFVGVHVIVLBAPQRVFVGVJVMVJVFVGVMVIVHVFVGVMSSZVICTZAEZCUAVHVN
      SZCAUBVPVQCVKVOLZVPVQVOAUCVOUDEZVRVPVQSZSCUEVSVRVKVOUFNZVTVKVOUDUGVGVPVHV
      FWAVMVGVPVHVFWAVMSZSZVGVPVHIVOBEZWCBVOAUHVFWDWBVFWDIVOBMNZWAVMVOBUIWAWEVM
      VKVOBUJUKULUMUNUOUPUNUQURUSUTVAVBVCVKBVDVE $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d X x $.  $d t x $.
    $( A Tarski's class that contains ` X ` is a Tarski's class. $)
    tmpts $p |- ( X e. A -> ( tarskiMap ` X ) e. Tarski ) $=
      ( vx vt cv ctarskim cfv ctsk wcel wceq fveq2 eleq1d cvv visset wa cab
      cint tarval2g wss c0 wne cin inab abid2 ineq2 inss2 sseq1 mpbiri syl
      ax-mp eqsstr3i wex wrex settrcon exancom df-rex bitr4i mpbir abn0
      intartar mp2an syl6eqel vtoclg ) CEZFGZHIZBFGZHICBAVDBJVEVGHVDBFKLVDMIZVF
      CNZVHVEVDDEZIZVJHIZOZDPZQZHDVDMRVNHSVNTUAZVOHIVNVKDPZVLDPZUBZHVKVLDUCVRHJ
      ZVSHSZDHUDVTVSVQHUBZJZWAVRHVQUEWCWAWBHSVQHUFVSWBHUGUHUIUJUKVPVMDULZWDVKDH
      UMZVHWEVIDVDMUNUJWDVLVKODULWEVKVLDUOVKDHUPUQURVMDUSURVNUTVAVBUJVC $.
      $( [17-Apr-2011] $)
  $}

  $c tar $. $( A function to study Tarski's classes. $)

  $( Extends class notation to include function ` tar ` . $)
  ctar $a class tar $.

  $( A function to study Tarski's classes.  See ~ valdom for its domain,
     ~ vtare for its value at ` (/) ` , ~ vtarsu for its value at a successor,
     ~ vtarl for its value at a limit ordinal. $)
  df-tar $a |- tar = { <. <. a , b >. , c >. | c = ( rec
       ( { <. x , y >. | y = ( ( ( { u | E. v e. x u C_ v } u.
         { u | E. v e. x u = ~P v } ) u.
          ~P x ) i^i ( tarskiMap ` a ) ) } , { a } ) |` b ) } $.

  ${
    $d X a b c x $.  $d X a b c y $.  $d Y a b c $.  $d a b c u $.
    $d a b c v $.
    $( The ` tar ` function as a recursive function. $)
    valtar $p |- ( ( X e. A /\ Y e. B ) -> ( tar ` <. X , Y >. ) = ( rec
       ( { <. x , y >. | y = ( ( ( { u | E. v e. x u C_ v } u.
         { u | E. v e. x u = ~P v } ) u.
          ~P x ) i^i ( tarskiMap ` X ) ) } , { X } ) |` Y ) ) $=
      ( vc va vb wcel wa cop ctar cfv cv wss wrex cab cpw wceq cun ctarskim
      cin copab csn crdg cres copab2 co df-tar fveq1i a1i df-opr eqcomi cvv
      elisset adantr adantl wfun con0 wfn rdgfnon fnfun syl resfunexg sylancom
      fveq2 ineq2d eqeq2d opabbidv rdgeq1 sneq rdgeq2 eqtrd reseq1 reseq2 cxp
      wrel cdm reloprab reldmoprab resid2 mp2an resoprab eqtr3i oprabval2g
      syl111anc 3eqtrd ) GELZHFLZMZGHNZOPZWNIQBQZDQZCQZRCAQZSDTWQWRUAUBCWSSDTUC
      WSUAUCZJQZUDPZUEZUBZABUFZXAUGZUHZKQZUIZUBZJKIUJZPZGHXKUKZWPWTGUDPZUEZUBZA
      BUFZGUGZUHZHUIZWOXLUBWMWNOXKABCDJKIULUMUNXLXMUBWMXMXLGHXKUOUPUNWMGUQLZHUQ
      LZXTUQLZXMXTUBWKYAWLGEURUSWLYBWKHFURUTWKWLXSVAZYCWMXSVBVCZYDYEWMXRXQVDUNV
      BXSVEVFXSHFVGVHJKIGHUQUQXIXTXKXSXHUIZUQXAGUBZXGXSUBXIYFUBYGXGXQXFUHZXSYGX
      EXQUBXGYHUBYGXDXPABYGXCXOWPYGXBXNWTXAGUDVIVJVKVLXFXEXQVMVFYGXFXRUBYHXSUBX
      AGVNXFXRXQVOVFVPXGXSXHVQVFXHHXSVRXKUQUQVSUIZXKXAUQLXHUQLMXJMJKIUJXKVTXKWA
      VTYIXKUBXJJKIWBXJJKIWCXKWDWEXJJKIUQUQWFWGWHWIWJ $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d X x $.  $d X y $.
    $( The domain of the ` tar ` function is the ordinal ` Y ` . $)
    valdom $p |- ( ( X e. A /\ Y e. On )
      -> dom ( tar ` <. X , Y >. ) = Y ) $=
      ( vy vw vz vx cop ctar cfv cv wss wrex cab cpw wceq cun ctarskim cin
      copab csn crdg cres wcel con0 wa cdm valtar dmres wfn wi rdgfnon fndm
      onss df-ss biimpi ineq2 eqeq1d syl5ibr syl5com adantl ax-mp syl5eq dmeq
      mpcom ) BCHIJZDKEKZFKZLFGKZMENVGVHOPFVIMENQVIOQBRJSPGDTZBUAZUBZCUCZPZBAUD
      ZCUEUDZUFZVFUGZCPZGDFEAUEBCUHVQVSVNVMUGZCPVQVTCVLUGZSZCVLCUIVLUEUJZVQWBCP
      ZUKVKVJULWCWAUEPZVQWDUEVLUMVPWEWDUKVOVPCUELZWEWDCUNWFWDWECUESZCPZWFWHCUEU
      OUPWEWBWGCWAUECUQURUSUTVAUTVBVCVNVRVTCVFVMVDURUSVE $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d X x $.  $d X y $.
    $( Value of ` tar ` at ` (/) ` .  JFM CLASSES1 th. 10. $)
    vtare $p |- ( ( X e. A /\ Y e. On /\ Y =/= (/) )
     -> ( ( tar ` <. X , Y >. ) ` (/) ) = { X } ) $=
      ( vy vw vz vx wcel con0 c0 wne w3a cop ctar cfv csn wceq cv wss wrex cab
      cpw cun ctarskim cin copab crdg cres on0eln0 biimpar 3adant1 fvres syl
      cvv snex a1i rdg0g 3ad2ant1 eqtrd wb wa valtar fveq1d eqeq1d 3adant3
      mpbird ) BAHZCIHZCJKZLZJBCMNOZOZBPZQZJDRERZFRZSFGRZTEUAVOVPUBQFVQTEUAUCVQ
      UBUCBUDOUEQGDUFZVMUGZCUHZOZVMQZVJWAJVSOZVMVJJCHZWAWCQVHVIWDVGVHWDVICUIUJU
      KJCVSULUMVGVHWCVMQZVIVGVMUNHZWEWFVGBUOUPVMUNVRUQUMURUSVGVHVNWBUTVIVGVHVAZ
      VLWAVMWGJVKVTGDFEAIBCVBVCVDVEVF $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d A u x y $.  $d A v x y $.  $d X u x y $.  $d X v x y $.  $d Y u x y $.
    $d Y v x y $.
    $( The parts and the powersets of the elements of ` tar ( A ) ` are
       elements of ` tar ( suc A ) ` .  As well as the parts of ` tar ( A ) `
       when they are elements of the smallest Tarski's class of which ` X ` is
       an element.  JFM CLASSES1 th. 11. $)
    vtarsu $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) -> ( ( tar `
    <. X , Y >. ) ` suc A ) = ( ( ( { u | E. v e. ( ( tar ` <. X , Y
    >. ) ` A ) u C_ v } u. { u | E. v e. ( ( tar ` <. X , Y >. ) ` A
    ) u = ~P v } ) u. ~P ( ( tar ` <. X , Y >. ) ` A ) ) i^i (
    tarskiMap ` X ) ) ) $=
      ( vy vx wcel con0 csuc w3a cop ctar cfv cv wss wrex cab cpw wceq cun
      ctarskim cin copab csn crdg cres valtar 3adant3 fveq1d fvres 3ad2ant3
      ordsuccl2 onelon syldan 3adant1 rdgsuc syl eqcomd fveq2d wa cvv fvex
      inex2 a1i rexeq abbidv uneq12d pweq ineq1d fvopabg sylancr eqtrd 3eqtrd
      ) EDIZFJIZCKZFIZLZVREFMNOZOVRGPBPZAPZQZAHPZRZBSZWBWCTUAZAWERZBSZUBZWETZUB
      ZEUCOZUDZUAHGUEZEUFZUGZFUHZOZVRWROZWDACWAOZRZBSZWHAXBRZBSZUBZXBTZUBZWNUDZ
      VTVRWAWSVPVQWAWSUAVSHGABDJEFUIZUJUKVSVPWTXAUAVQVRFWRULUMVTXACWROZWPOZCWSO
      ZWPOZXJVTCJIZXAXMUAVQVSXPVPVQVSCFIZXPCFUNZFCUOUPUQWQCWPURUSVTXLXNWPVTXQXL
      XNUAVQVSXQVPXRUQXQXNXLCFWRULUTUSVAVTXOXBWPOZXJVTXNXBWPVTCWSWAVPVQWSWAUAVS
      VPVQVBWAWSXKUTUJUKVAVTXBVCIXJVCIZXSXJUACWAVDXTVTWNXIEUCVDVEVFHGXBWOXJVCVC
      WEXBUAZWMXIWNYAWKXGWLXHYAWGXDWJXFYAWFXCBWDAWEXBVGVHYAWIXEBWHAWEXBVGVHVIWE
      XBVJVIVKVLVMVNVOVO $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d X x $.  $d X y $.
    $( The value of ` tar ` at a limit ordinal.  JFM CLASSES1 th. 12. $)
    vtarl $p |- ( ( X e. B /\ Y e. On /\ ( A e. Y /\ Lim A ) )
      -> ( ( tar ` <. X , Y >. ) ` A ) = U. ( ( tar ` <. X , Y >. ) " A ) ) $=
      ( vy vu vv vx wcel con0 wlim wa w3a cop ctar cfv cv wss wrex cab cpw
      wceq cun ctarskim cin copab csn crdg cres cima cuni valtar fveq1d
      3adant3 simp3l fvres syl rdglim 3ad2ant3 word eloni 3ad2ant2 imresord
      syl11anc eqcomd imaeq1d eqtr3d unieqd eqtrd 3eqtrd ) CBIZDJIZADIZAKZLZMZA
      CDNOPZPZAEQFQZGQZRGHQZSFTVSVTUAUBGWASFTUCWAUAUCCUDPUEUBHEUFZCUGZUHZDUIZPZ
      AWDPZVQAUJZUKZVKVLVRWFUBVOVKVLLAVQWEHEGFBJCDULZUMUNVPVMWFWGUBVKVLVMVNUOZA
      DWDUPUQVPWGWDAUJZUKZWIVOVKWGWMUBVLWCADWBURUSVPWLWHVPWEAUJZWLWHVPDUTZVMWNW
      LUBVLVKWOVODVAVBWKADWDVCVDVPWEVQAVPVQWEVKVLVQWEUBVOWJUNVEVFVGVHVIVJ $.
      $( [20-Mar-2011] $)
  $}


  ${
    $d A b c $.  $d B b $.  $d X b c $.  $d X c u $.  $d X c v $.  $d X x $.
    $d X y $.  $d Y b c $.  $d Y c u $.  $d Y c v $.
    $( The sequence ` tar ` has its values in a Tarski's class. $)
    tartarmap $p |- ( ( X e. A /\ Y e. On /\ suc B e. Y ) ->
      ( ( tar ` <. X , Y >. ) ` B ) C_ ( tarskiMap ` X ) ) $=
      ( vb vc vu vv vy vw vz vx csuc con0 wcel w3a cop ctar cfv ctarskim wss
      onelon 3adant1 wi sucelon cv c0 wceq suceq eleq1d 3anbi3d fveq2 sseq1d
      imbi12d csn wne ne0i vtare syl3an3 elttar 3ad2ant1 wb snssg mpbid
      eqsstrd wrex cab cpw cun cin inss2 simp1 simp2 wtr word eloni ordtr syl
      3ad2ant2 simp3 trsuc syl11anc 3jca 3ad2ant3 vtarsu mpbiri 3exp wlim wral
      cima cuni wa simp31 simp32 ordsuccl2 adantl simpl jca 3adant2 vtarl
      syl111anc r19.27av ordon ordtr1 ax-mp sylbir ancoms ordsucelsuc simpl1
      simpl2 a1i com12 ex com13 syl6 3imp1 expcom syl6bi pm2.43a pm5.31 com3r
      imp impcom ralimiaa r19.26 3simpa ralimi wfun cdm copab crdg cres valtar
      wfn rdgfnon fnfun funres funeq 3adant3 ordsuccl3 valdom sseqtr4d
      funimass4 fvex elpw ralbii syl6bb uniss unipw syl6sseq syl6bir com23
      sylbi pm2.43d 3imp tfinds mpcom ) BMZNOZCAOZDNOZUURDOZPZBCDQRSZSZCTSZUAZU
      VAUVBUUSUUTDUURUBUCUUSBNOUVCUVGUDZBUEUUTUVAEUFZMZDOZPZUVIUVDSZUVFUAZUDUUT
      UVAUGMZDOZPZUGUVDSZUVFUAZUDUUTUVAFUFZMZDOZPZUVTUVDSZUVFUAZUDZUUTUVAUWAMZD
      OZPZUWAUVDSZUVFUAZUDUVHEFBUVIUGUHZUVLUVQUVNUVSUWLUVKUVPUUTUVAUWLUVJUVODUV
      IUGUIUJUKUWLUVMUVRUVFUVIUGUVDULUMUNUVIUVTUHZUVLUWCUVNUWEUWMUVKUWBUUTUVAUW
      MUVJUWADUVIUVTUIUJUKUWMUVMUWDUVFUVIUVTUVDULUMUNUVIUWAUHZUVLUWIUVNUWKUWNUV
      KUWHUUTUVAUWNUVJUWGDUVIUWAUIUJUKUWNUVMUWJUVFUVIUWAUVDULUMUNUVIBUHZUVLUVCU
      VNUVGUWOUVKUVBUUTUVAUWOUVJUURDUVIBUIUJUKUWOUVMUVEUVFUVIBUVDULUMUNUVQUVRCU
      OZUVFUVPUUTUVADUGUPUVRUWPUHDUVOUQACDURUSUVQCUVFOZUWPUVFUAZUUTUVAUWQUVPCAU
      TVAUUTUVAUWQUWRVBUVPCUVFAVCVAVDVEUVTNOZUWFUWIUWKUWSUWFUWIPZUWKGUFZHUFZUAH
      UWDVFGVGUXAUXBVHUHHUWDVFGVGVIUWDVHVIZUVFVJZUVFUAZUXCUVFVKUWTUWCUWKUXEVBUW
      IUWSUWCUWFUWIUUTUVAUWBUUTUVAUWHVLUUTUVAUWHVMUWIDVNZUWHUWBUVAUUTUXFUWHUVAD
      VOZUXFDVPZDVQVRVSUUTUVAUWHVTDUWAWAWBWCWDUWCUWJUXDUVFHGUVTACDWEUMVRWFWGUVI
      WHZUWFFUVIWIZUVLUVNUXIUXJUVLPZUVMUVDUVIWJZWKZUVFUXKUUTUVAUVIDOZUXIWLZUVMU
      XMUHUXIUXJUUTUVAUVKWMUXIUXJUUTUVAUVKWNUXIUVLUXOUXJUXIUVLWLUXNUXIUVLUXNUXI
      UVAUVKUXNUUTUVIDWOUCWPUXIUVLWQWRWSUVIACDWTXAUXIUXJUVLUXMUVFUAZUXJUVLUXPUD
      ZUDUXIUXJUVLUXPUXJUVLUXQUXJUVLWLUWFUVLWLZFUVIWIZUXQUWFUVLFUVIXBUXSUWEUWCW
      LZFUVIWIZUXQUXRUXTFUVIUXRUVTUVIOZUXTUWFUVLUYBUXTUDUVLUYBUWFUXTUVLUYBUWCUW
      FUXTUDUYBUVLUWCUVLUVIVOZUYBUVLUWCUDZUDUVAUVKUYCUUTUVKUVAUYCUVKUVAWLZUVJNO
      ZUYCNVOUYEUYFUDXCUVJDNXDXEUYFUVINOUYCUVIUEUVIVPXFVRXGUCUYCUYBUWAUVJOZUYDU
      VTUVIXHUVLUYGUWCUVLUYGWLUUTUVAUWBUUTUVAUVKUYGXIUUTUVAUVKUYGXJUUTUVAUVKUYG
      UWBUUTUVAUXGUVKUYGUWBUDUDUVAUXGUDUUTUXHXKUYGUVKUXGUWBUYGUVKUXGUWBUDUXGUYG
      UVKWLUWBUWAUVJDXDXLXMXNXOXPWCXQXRVRXSUWFUWCUXTUWCUWFUWCUXTUDUWCUWEUWCXTXM
      XSXOYAYBYCYDUYAUWEFUVIWIZUWCFUVIWIZWLUXQUWEUWCFUVIYEUYIUYHUXQUYIUUTUVAWLZ
      FUVIWIZUYHUXQUDUWCUYJFUVIUUTUVAUWBYFYGUYKUVLUYHUXPUYKUVLUVDYHZUVIUVDYIZUA
      ZWLZUYHUXPUDUYKUVLUYOUYKUVLWLUYLUYNUVLUYLUYKUUTUVAUYLUVKUYJUVDIUFJUFZKUFZ
      UAKLUFZVFJVGUYPUYQVHUHKUYRVFJVGVIUYRVHVIUVFVJUHLIYJZUWPYKZDYLZUHZUYLLIKJA
      NCDYMVUBUYLVUAYHZUYTYHZVUCUYTNYNVUDUWPUYSYONUYTYPXEDUYTYQXEUVDVUAYRWFVRYS
      WPUVLUYNUYKUVLUVIDUYMUVAUVKUVIDUAUUTUVIDYTUCUUTUVAUYMDUHUVKACDUUAYSUUBWPW
      RXMUYOUYHUXLUVFVHZUAZUXPUYOVUFUWDVUEOZFUVIWIUYHFUVIVUEUVDUUCVUGUWEFUVIUWD
      UVFUVTUVDUUDUUEUUFUUGVUFUXMVUEWKUVFUXLVUEUUHUVFUUIUUJUUKXOUULVRYCUUMVRVRX
      MUUNXKUUOVEWGUUPXFUUQ $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d A t $.  $d X t $.
    $( If ` A ` belongs to the smallest Tarski's class that contains ` X ` so
       does ` ~P A ` .  CLASSES1 th. 7. $)
    pwtsm $p |- ( ( X e. B /\ A e. ( tarskiMap ` X ) )
      -> ~P A e. ( tarskiMap ` X ) ) $=
      ( vt wcel ctarskim cfv wa cpw cv wi ctsk wral btmp biimpd ex pm2.43d imp
      tskpw imim2d ralimia syl cvv wb pwexg sylan2 mpbird ) CBEZACFGZEZHZAIZUIE
      ZCDJZEZULUNEZKZDLMZUKUOAUNEZKZDLMZURUHUJVAUHUJVAUHUJUJVAKUKUJVADCABUINOPQ
      RUTUQDLUNLEZUSUPUOVBUSUPAUNSPTUAUBUJUHULUCEUMURUDAUIUEDCULBUCNUFUG $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A t $.  $d X t $.
    $( If ` A ` belongs to the smallest Tarski's class that contains ` X ` so
       does the subsets of ` A ` .  CLASSES1. th. 6. $)
    subtsm $p |- ( ( X e. B /\ A e. ( tarskiMap ` X ) )
      -> ~P A C_ ( tarskiMap ` X ) ) $=
      ( vt wcel ctarskim cfv wa cpw wss cv wi ctsk wral btmp tskpwss ex imim2d
      ralimia syl6bi pm2.43d imp cvv wb pwexg bpmp sylan2 mpbird ) CBEZACFGZEZH
      ZAIZUJJZCDKZEZUMUOJZLZDMNZUIUKUSUIUKUSUIUKUKUSLULUKUPAUOEZLZDMNUSDCABUJOV
      AURDMUOMEZUTUQUPVBUTUQAUOPQRSTQUAUBUKUIUMUCEUNUSUDAUJUEDCUMBUCUFUGUH $.
      $( [17-Apr-2011] $)
  $}

  $( If ` A ` is a subset of the smallest Tarski's class that contains ` X `
     then it is equipotent to this class or it belongs to it.  CLASSES1 th.
     8. $)
  subtareqbe $p |- ( ( X e. B /\ A e. ~P ( tarskiMap ` X ) ) ->
      ( A ~~ ( tarskiMap ` X ) \/ A e. ( tarskiMap ` X ) ) ) $=
    ( wcel ctarskim cfv ctsk cpw cen wbr wo tmpts tsken sylan ) CBDCEFZGDAOHDAO
    IJAODKBCLAOMN $.
    $( [17-Apr-2011] $)

  $c tr $.

  $( $)

  $( Extend class notation to include the function whose value is the
     transitive closure of its operand. $)
  ctr $a class tr $.

  ${
    $d x y a z w $.
    $( The transitive closure of a set. $)
    df-trcls $a |- tr = { <. x , y >. | y = U_ a e. om ( ( rec ( { <. z , w >.
      | w = ( z u. U. z ) } , x ) |` om ) ` a ) } $.
  $}

  ${
    $d A a x y $.  $d a w x y z $.
    $( The transitive closure of a set A. $)
    trclval $p |- ( A e. B
    -> ( tr ` A ) = U_ a e. om
     ( ( rec ( { <. z , w >. | w = ( z u. U. z ) } , A ) |` om ) ` a ) ) $=
      ( vx vy wcel cvv com cv cuni cun wceq copab crdg cres cfv ciun ctr
      elisset wral omex fvex a1i rgen iunexg mp2an rdgeq2 reseq1 syl fveq1d
      adantr iuneq2dv wa df-trcls wrel relopab resid ax-mp resopab 3eqtr2i
      fvopab4g sylancl ) CDHCIHEJEKZBKAKZVFLMNABOZCPZJQZRZSZIHZCTRVKNCDUAJIHVJI
      HZEJUBVLUCVMEJVMVEJHZVEVIUDUEUFEJVJIIUGUHFGCEJVEVGFKZPZJQZRZSZVKIITVOCNZE
      JVRVJVTVRVJNVNVTVEVQVIVTVPVHNVQVINVOCVGUIVPVHJUJUKULUMUNTGKVSNZFGOZWBIQZV
      OIHWAUOFGOFGABEUPWBUQWCWBNWAFGURWBUSUTWAFGIVAVBVCVD $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A u v $.  $d A v z $.  $d B v $.  $d C u v $.  $d C v z $.  $d X u v $.
    $d X v z $.  $d Y u v $.  $d Y v z $.
    $( C belongs to the value of ` tar ` at a successor of ` A ` iff it is a
       part of ` tar ` at ` A ` , the powerset of an element or a part of an
       element of ` tar ` at ` A ` .  CLASSES1 th. 13 $)
    vtarsuelt $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) -> ( C e. ( ( tar
    ` <. X , Y >. ) ` suc A ) <-> ( ( C C_ ( ( tar ` <. X , Y >. ) ` A ) /\
    C e. ( tarskiMap ` X ) ) \/ E. z e. ( ( tar ` <. X , Y >. ) ` A )
    ( C C_ z \/ C = ~P z ) ) ) ) $=
      ( vu vv wcel con0 csuc w3a cop ctar cfv cv wss wrex cab cpw wceq cun
      ctarskim cin wa wo vtarsu eleq2d wb elin a1i elun anbi1d andir orbi1d
      orcom wi elpwg pm5.32rd sseq1 rexbidv elabg eqeq1 orbi12d r19.43 bicomi
      tartarmap sseld imp subtsm visset elpw2 ssel com12 sylbir syl5com ex
      com23 a1d 3ad2ant1 pwtsm eleq1 imbi2d syl5ibrcom adantr jaoi com13 mpcom
      pm4.71 sylib bicomd rexbidva r19.41v sseq2 pweq eqeq2d cbvrexv 3bitr3g
      3bitrd syl5bb bitrd ) ECIZFJIZBKZFIZLZDXDEFMNOZOZIDGPZHPZQZHBXGOZRZGSZXIX
      JTZUAZHXLRZGSZUBZXLTZUBZEUCOZUDZIZDXLQZDYBIZUEZDAPZQZDYHTZUAZUFZAXLRZUFZX
      FXHYCDHGBCEFUGUHXFYDDYAIZYFUEZDXSIZDXTIZUFZYFUEZYNYDYPUIXFDYAYBUJUKXFYOYS
      YFYOYSUIXFDXSXTULUKUMXFYTYQYFUEZYRYFUEZUFZDXNIZDXRIZUFZYFUEZUUBUFZYNYTUUC
      UIXFYQYRYFUNUKXFUUAUUGUUBXFYQUUFYFYQUUFUIXFDXNXRULUKUMUOUUHUUBUUGUFXFYNUU
      GUUBUPXFUUBYGUUGYMXFYFYRYEYFYRYEUIUQXFDXLYBURUKUSXFUUGDXJQZHXLRZDXOUAZHXL
      RZUFZYFUEUUIUUKUFZHXLRZYFUEZYMXFYFUUFUUMYFUUFUUMUIUQXFYFUUDUUJUUEUULXMUUJ
      GDYBXIDUAZXKUUIHXLXIDXJUTVAVBXQUULGDYBUUQXPUUKHXLXIDXOVCVAVBVDUKUSXFUUMUU
      OYFUUMUUOUIXFUUOUUMUUIUUKHXLVEVFUKUMXFUUNYFUEZHXLRUUOUUPYMXFUURUUNHXLXFXJ
      XLIZUEZUUNUURUUTUUNYFUQZUUNUURUIXJYBIZUUTUVAXFUUSUVBXFXLYBXJCBEFVGVHVIUUN
      UUTUVBYFUUIUUTUVBYFUQZUQUUKUUTUUIUVCXFUUSUUIUVCUQZXBXCUUSUVDUQXEXBUVDUUSX
      BUVBUUIYFXBUVBUUIYFUQXBUVBUEXOYBQZUUIYFXJCEVJUUIDXOIZUVEYFUQDXJHVKVLUVEUV
      FYFXOYBDVMVNVOVPVQVRVSVTVIVNUUTUUKUVCXFUUKUVCUQZUUSXBXCUVGXEXBUVCUUKUVBXO
      YBIZUQXBUVBUVHXJCEWAVQUUKYFUVHUVBDXOYBWBWCWDVTWEVNWFWGWHUUNYFWIWJWKWLUUNY
      FHXLWMUUNYLHAXLXJYHUAZUUIYIUUKYKXJYHDWNUVIXOYJDXJYHWOWPVDWQWRWSVDWTWSWSXA
      $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A z $.  $d C z $.  $d X z $.  $d Y z $.  $d Z z $.
    $( If ` C ` is a part of an element of our tar function at ` A ` then ` C `
       is an element or tar at ` suc A ` .  CLASSES1 th. 14 $)
    partarelt1 $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( ( C C_ Z /\ Z e. ( ( tar ` <. X , Y >. ) ` A ) ) ->
          C e. ( ( tar ` <. X , Y >. ) ` suc A ) ) ) $=
      ( vz wss cop ctar cfv wcel wa con0 csuc w3a cpw wceq wo wi orc cv wrex
      sseq2 pweq eqeq2d orbi12d rcla4ev ctarskim olc vtarsuelt syl5ibrcom syl
      ex mpan9 com12 ) CFHZFADEIJKZKZLZMDBLENLAOZELPZCVAURKLZUQUQCFQZRZSZUTVBVC
      TZUQVEUAUTVFVGUTVFMCGUBZHZCVHQZRZSZGUSUCZVGVLVFGFUSVHFRZVIUQVKVEVHFCUDVNV
      JVDCVHFUEUFUGUHVMVCVBCUSHCDUIKLMZVMSVMVOUJGABCDEUKULUMUNUOUP $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A u v $.  $d X u v $.  $d Y u v $.  $d Z u v $.
    $( If ` Z ` is an element of our tar function at ` A ` then ` ~P Z ` is an
       element or tar at ` suc A ` .  CLASSES1 th. 15 $)
    partarelt2 $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( Z e. ( ( tar ` <. X , Y >. ) ` A ) ->
          ~P Z e. ( ( tar ` <. X , Y >. ) ` suc A ) ) ) $=
      ( vu vv wcel con0 csuc w3a cop ctar cfv cpw wa cv wss wrex cab wceq cun
      ctarskim cin simpr eqidd pweq eqeq2d rcla4ev syl11anc wb cvv pwexg eqeq1
      rexbidv elabg syl adantl mpbird elun2 elun1 3syl simpl1 tartarmap sseld
      imp pwtsm elin sylanbrc vtarsu adantr eleqtrrd ex ) CBHZDIHZAJZDHZKZEACDL
      MNZNZHZEOZVPVSNZHVRWAPZWBFQZGQZRGVTSFTZWEWFOZUAZGVTSZFTZUBZVTOZUBZCUCNZUD
      ZWCWDWBWNHZWBWOHZWBWPHWDWBWKHZWBWLHWQWDWSWBWHUAZGVTSZWDWAWBWBUAZXAVRWAUEW
      DWBUFWTXBGEVTWFEUAWHWBWBWFEUGUHUIUJWAWSXAUKZVRWAWBULHXCEVTUMWJXAFWBULWEWB
      UAWIWTGVTWEWBWHUNUOUPUQURUSWBWKWGUTWBWLWMVAVBWDVNEWOHZWRVNVOVQWAVCVRWAXDV
      RVTWOEBACDVDVEVFEBCVGUJWBWNWOVHVIVRWCWPUAWAGFABCDVJVKVLVM $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d X x $.  $d Y x $.
    $( All the element of ` tar ` at ` A ` are elements of ` tar ` at
       ` suc A ` .  CLASSES1 th. 18 $)
    tareltsuc $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( ( tar ` <. X , Y >. ) ` A ) C_
         ( ( tar ` <. X , Y >. ) ` suc A ) ) $=
      ( vx wcel con0 csuc w3a cop ctar cfv cv wss ssid partarelt1 mpani ssrdv
      ) CBFDGFAHZDFIZEACDJKLZLZSUALZTEMZUDNUDUBFUDUCFUDOABUDCDUDPQR $.
      $( [13-Apr-2011] $)
  $}

  $( An element of the intersection of a Tarski's class with the class of the
     ordinal numbers is a part of the intersection. $)
  eltintpar $p |- ( T e. Tarski -> ( A e. ( On i^i T )
      -> A C_ ( On i^i T ) ) ) $=
    ( ctsk wcel con0 cin wss wa wi elin onss ad2antrr word wtr eloni ordtr
    tartrel 3exp com3l 3syl imp31 jca ex sylbi impcom ssin sylib ) BCDZAEBFZDZA
    UIGZUHUJHAEGZABGZHZUKUJUHUNUJAEDZABDZHZUHUNIAEBJUQUHUNUQUHHULUMUOULUPUHAKLU
    OUPUHUMUOAMANZUPUHUMIIAOAPUHURUPUMUHURUPUMABQRSTUAUBUCUDUEAEBUFUGUC $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The intersection of a Tarski's class with the class of the ordinal
       numbers is an ordinal number. $)
    inttaror $p |- ( T e. Tarski -> ( On i^i T ) e. On ) $=
      ( vx ctsk wcel con0 cin cvv cv wss wa wral incom inex1g syl5eqelr inss1
      sseli adantl wi elin word wtr eloni ordtr onss w3a tartrel ssin biimpi
      ex syl5com 3exp com14 syl com12 3syl pm2.43i imp sylbi impcom jca
      r19.21aiv celsor syl11anc ) ACDZEAFZGDBHZEDZVFVEIZJZBVEKVEEDVDVEAEFGAELAE
      CMNVDVIBVEVDVFVEDZVIVDVJJVGVHVJVGVDVEEVFEAOPQVJVDVHVJVGVFADZJVDVHRZVFEASV
      GVKVLVGVKVLRZVGVFTVFUAZVGVMRVFUBVFUCVGVNVMVGVFEIZVNVMRVFUDVDVNVKVOVHVDVNV
      KVOVHRVDVNVKUEVFAIZVOVHVFAUFVOVPVHVOVPJVHVFEAUGUHUIUJUKULUMUNUOUPUQURUSUT
      UIVABVEGVBVC $.
      $( [20-Apr-2011] $)
  $}

  $( The intersection of a Tarski's class and the ordinal numbers is equipotent
     to the Tarski's class.  JFM CLASSES2. . $)
  inttarcar $p |- ( T e. Tarski -> ( On i^i T ) ~~ T ) $=
    ( ctsk wcel con0 cin wss cen wbr inss2 wo wa tarax3a wi ax-1 inttaror elin
    elirr pm2.21i sylbir ex syl adantr com12 jaoi mpcom mpan2 ) ABCZDAEZAFZUHAG
    HZDAIUJUHACZJUGUIKZUJUHALUJULUJMUKUJULNULUKUJUGUKUJMZUIUGUHDCZUMAOUNUKUJUNU
    KKUHUHCZUJUHDAPUOUJUHQRSTUAUBUCUDUEUF $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The cardinal of the intersection of a Tarski's class with the class of
       the ordinal numbers. $)
    carinttar $p |- ( T e. Tarski
      -> ( card ` ( On i^i T ) ) = ( On i^i T ) ) $=
      ( vx ctsk wcel con0 cin cv csdm wbr wral ccrd cfv wceq inttaror wa cdom
      cen wn wss eltintpar cvv wi incom inex1g syl5eqel ssdom2g syl syld imp
      inttarcar elin tsksdom sdomnen entr expcom con3d a1i com3r mpcom adantl
      sylbi com13 brsdom sylanbrc ex r19.21aiv iscard ) ACDZEAFZEDBGZVIHIZBVIJV
      IKLVIMANVHVKBVIVHVJVIDZVKVHVLOVJVIPIZVJVIQIZRZVKVHVLVMVHVLVJVISZVMVJATVHV
      IUADVPVMUBVHVIAEFUAEAUCAECUDUEVJVIUAUFUGUHUIVHVLVOVIAQIZVHVLVOUBAUJVLVHVQ
      VOVLVJEDZVJADZOVHVQVOUBZUBZVJEAUKVSWAVRVHVSVTVJAHIZVHVSOZVTVJAULWBVJAQIZR
      ZWCVTUBVJAUMWCVQWEVOVQWEVOUBUBWCVQVNWDVNVQWDVJVIAUNUOUPUQURUGUSUOUTVAVBUS
      UIVJVIVCVDVEVFBVIVGVD $.
      $( [20-Apr-2011] $)
  $}

  $( The cardinal of a Tarski's class equals the intersection of the Tarski's
     class with the class of the ordinal numbers.  CLASSES2 th. 10. $)
  carinttar2 $p |- ( T e. Tarski
      -> ( card ` T ) = ( On i^i T ) ) $=
    ( ctsk wcel ccrd cfv con0 cin wceq cen wbr inttarcar ensymg mpd wb
    inttaror carden mpdan mpbird carinttar eqtrd ) ABCZADEZFAGZDEZUCUAUBUDHZAUC
    IJZUAUCAIJUFAKUCABLMUAUCFCUEUFNAOAUCBFPQRAST $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The cardinal of an element of a Tarski's class belongs to the Tarski's
       class. th. 12 CLASSES2 $)
    cardtar $p |- ( ( T e. Tarski /\ A e. T ) -> ( card ` A ) e. T ) $=
      ( ctsk wcel wa ccrd cfv con0 cin wceq carinttar2 adantr tarax3d3 eleq2
      elin simprbi syl6bi sylc ) BCDZABDZEBFGZHBIZJZAFGZUADZUDBDZSUCTBKLABMUCUE
      UDUBDZUFUAUBUDNUGUDHDUFUDHBOPQR $.
      $( [20-Apr-2011] $)

    $( The cardinal of a Tarski's class is a limit ordinal.  CLASSES2 th.
       21. $)
    cartarlim $p |- ( ( T e. Tarski /\ T =/= (/) ) -> Lim ( card ` T ) ) $=
      ( vx ctsk wcel c0 wne wa ccrd cfv wlim con0 cin word cv csuc wral w3a
      inttaror adantr cvv wb incom inex1g syl5eqel elong syl mpbid tsk0 0elon
      jctil elin sylibr wi suceloni ad2antrr visset elon tarsuc2 3exp com3l
      sylbi imp31 sylanbrc ex com12 r19.21aiv 3jca dflim4 wceq carinttar2
      limeq mpbird ) ACDZAEFZGZAHIZJZKALZJZVOVRMZEVRDZBNZOZVRDZBVRPZQVSVOVTWAWE
      VOVRKDZVTVMWFVNARSVOVRTDZWFVTUAVMWGVNVMVRAKLTKAUBAKCUCUDSVRTUEUFUGVOEKDZE
      ADZGWAVOWIWHAUHUIUJEKAUKULVOWDBVRWBVRDZVOWDWJWBKDZWBADZGZVOWDUMWBKAUKWMVO
      WDWMVOGWCKDZWCADZWDWKWNWLVOWBUNUOWKWLVOWOWKWBMZWLVOWOUMUMWBBUPUQVOWPWLWOV
      MWPWLWOUMUMVNVMWPWLWOWBAURUSSUTVAVBWCKAUKVCVDVAVEVFVGBVRVHULVOVPVRVIZVQVS
      UAVMWQVNAVJSVPVRVKUFVL $.
      $( [20-Apr-2011] $)
  $}

  ${
    $( An element of the cardinal of the Tarski's class ` T ` is an element of
       ` T ` . th. 14 CLASSES2. $)
    elcarelcl $p |- ( ( T e. Tarski /\ A e. ( card ` T ) )
    -> A e. T ) $=
      ( ctsk wcel ccrd cfv con0 cin wceq wi carinttar2 eleq2 elin simprbi
      syl6bi syl imp ) BCDZABEFZDZABDZRSGBHZIZTUAJBKUCTAUBDZUASUBALUDAGDUAAGBMN
      OPQ $.
      $( [20-Nov-2011] $)
  $}

  ${
    $d A f x $.  $d T f x $.
    $( Consider functions whose domain ` A ` is an element of a transitive
       Tarski's class ` T ` and whose range is ` T ` , then they are elements
       of ` T ` .  CLASSES2 th. 23. $)
    fnctartar $p |- ( ( T e. Tarski /\ Tr T /\ A e. T ) -> ( T ^m A ) C_ T ) $=
      ( vf vx ctsk wcel wtr w3a cv cmap co wi wal wss wa wf elmapg csdm wbr
      simpr1 wel cxp fssxp ssel c1st cfv c2nd cop wceq elxp6 simp31 trel exp3a
      com23 imp 3adant1 com12 adantr 3adant2 simp1r tskop syl111anc 3exp eleq1
      imbi2d syl5ibr sylbi syl6com com4l mpcom 19.21aiv dfss2 sylibr wfn wfun
      cdm ffn fnfun fndm jca cen visset fundmen breq1 tsksdom cvv sdomen1
      biimpd mpan syl5com syl6bi mpan9 3syl 3jca ex tskssel syl6 pm2.43a ) BEFZ
      BGZABFZHZCIZBAJKZFZXCBFZLZCMXDBNXBXGCXEXBXFWSXAXEXBXFLZLWTWSXAOZXEABXCPZX
      HBAXCEBQXJXBWSXCBNZXCBRSZHZXFXJXBXMXJXBOZWSXKXLXJWSWTXATXNDCUAZDIZBFZLZDM
      XKXNXRDXJXBXRXCABUBZNZXJXBXRLABXCUCXOXTXJXBXQXTXOXPXSFZXJXBXQLZLZXCXSXPUD
      YAXPXPUEUFZXPUGUFZUHZUIZYDAFZYEBFZOZOYCXPABUJYGYJYCYJYCYGXJXBYFBFZLZLYJXJ
      XBYKYJXJXBHWSYDBFZYIYKYJXJWSWTXAUKYJXBYMXJYJXBYMYHXBYMLYIXBYHYMWTXAYHYMLZ
      WSWTXAYNWTYHXAYMWTYHXAYMBYDAULUMUNUOUPUQURUOUSYHYIXJXBUTYDYEBVAVBVCYGYBYL
      XJYGXQYKXBXPYFBVDVEVEVFUOVGVHVIVJUOVKDXCBVLVMXJXBXLXJXCAVNZXCVOZXCVPZAUIZ
      OXBXLLZABXCVQYOYPYRAXCVRAXCVSVTYPYQXCWASZYRYSXCCWBZWCYRYTAXCWASZYSYQAXCWA
      WDXBUUBXLWSXAUUBXLLWTXIABRSZUUBXLABWEXCWFFZUUBUUCXLLUUAUUDUUBOUUCXLAXCBWF
      WGWHWIWJUSUQWKWLWMUOWNWOXCBWPWQWKUSWRVKCXDBVLVM $.
      $( [26-Sep-2011] $)
  $}

  ${
    $d A f x $.  $d T f x $.
    $( Consider functions whose domain ` A ` is an element and a part of a
       Tarski's class ` T ` and whose range is ` T ` , then they are elements
       of ` T ` .  CLASSES2 th. 23. $)
    fnctartar2 $p |- ( ( T e. Tarski /\ A C_ T /\ A e. T ) -> ( T ^m A ) C_ T )
        $=
      ( vf vx ctsk wcel wss w3a cv cmap co wi wal csdm wbr wf wb elmapg
      3adant2 wa simpr1 wel cxp fssxp ssel c1st cfv c2nd cop wceq elxp6 simp31
      3ad2ant2 com12 adantr imp simp1r tskop syl111anc 3exp eleq1 imbi2d
      syl5ibr sylbi syl6com com4l mpcom 19.21aiv dfss2 sylibr wfn wfun cdm ffn
      jca fnfun fndm anim12i cen visset fundmen breq1 tsksdom cvv sdomen1
      biimpd mpan syl5com syl6bi mpan9 3syl 3jca ex pm2.43a tskssel syl6 ) BEFZ
      ABGZABFZHZCIZBAJKZFZXABFZLZCMXBBGWTXECWTXCWQXABGZXABNOZHZXDXCWTXHWTXCABXA
      PZWTXHLWQWSXCXIQWRBAXAEBRSXIWTXHXIWTTZWQXFXGXIWQWRWSUAXJDCUBZDIZBFZLZDMXF
      XJXNDXIWTXNXAABUCZGZXIWTXNLABXAUDXKXPXIWTXMXPXKXLXOFZXIWTXMLZLZXAXOXLUEXQ
      XLXLUFUGZXLUHUGZUIZUJZXTAFZYABFZTZTXSXLABUKYCYFXSYFXSYCXIWTYBBFZLZLYFXIWT
      YGYFXIWTHWQXTBFZYEYGYFXIWQWRWSULYFWTYIXIYFWTYIYDWTYILYEWTYDYIWRWQYDYILWSA
      BXTUEUMUNUOUPSYDYEXIWTUQXTYABURUSUTYCXRYHXIYCXMYGWTXLYBBVAVBVBVCUPVDVEVFV
      GUPVHDXABVIVJXIWTXGXIXAAVKZYJTXAVLZXAVMZAUJZTWTXGLZXIYJYJABXAVNZYOVOYJYKY
      JYMAXAVPAXAVQVRYKYLXAVSOZYMYNXACVTZWAYMYPAXAVSOZYNYLAXAVSWBWTYRXGWQWSYRXG
      LWRWQWSTABNOZYRXGABWCXAWDFZYRYSXGLYQYTYRTYSXGAXABWDWEWFWGWHSUNWIWJWKUPWLW
      MWIWNXABWOWPVHCXBBVIVJ $.
      $( [27-Sep-2011] $)
  $}

  $( If the cardinal of ` A ` of a part of ` T ` is less than ` T ` . a
     function from ` A ` to ` T ` is a part of ` T ` .  CLASSES2 th. 23. $)
  fnctartar3 $p |- ( ( T e. Tarski /\ A C_ T /\
      ( card ` A ) e. ( card ` T ) ) -> ( T ^m A ) C_ T ) $=
    ( ctsk wcel wss ccrd cfv cmap co csdm wbr wa cvv wb ssexg ancoms simpl
    cardsdom syl11anc biimp3a tskssel syld3an3 fnctartar2 ) BCDZABEZAFGBFGDZABD
    ZBAHIBEUDUEUFABJKZUGUDUEUFUHUDUELAMDZUDUFUHNUEUDUIABCOPUDUEQABMCRSTABUAUBAB
    UCUB $.
    $( [20-Nov-2011] $)

