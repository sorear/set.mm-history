$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Tarski's classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Tarski $. $( The class of Tarski's classes. $)
  $c tarskiMap $.
  $( A function whose value is the smallest Tarski's classe
     containing a particular set. $)


  $( Extend class definition to include the class of all Tarski's classes. $)
  ctarski $a class Tarski $.

  $( Extend class definition to include the map whose value is the smallest
     Tarski's class. $)
  ctarskim $a class tarskiMap $.

  ${
    $d y z w $.
    $( The class of all Tarski's classes.  Tarski's classes is a phrase coined
       by Grzegorz Bancerek in his article "Tarski's Classes and Ranks" Journal
       of Formalized Mathematics.  Vol 1 no 3 May-August 1990.  A Tarski's
       class is a set whose existence is ensured by Tarski's axiom A (see
       ~ ax-groth and the equivalent axioms).  Axiom A was first presented in
       Tarski's article:  "&Uuml;ber unerreichbare Kardinalzahlen".  Tarski had
       invented the axiom A to enable ZFC to manage inaccessible cardinals.
       Later Grothendieck invented the concept of Grothendieck's universes and
       showed they were equal to transitive Tarski's classes. $)
    df-tsk $a |- Tarski = { y | ( A. z e. y ( ~P z C_ y
      /\ E. w e. y ~P z C_ w ) /\ A. z e. ~P y ( z ~~ y \/ z e. y ) ) } $.
  $}

  ${
    $d w y z $.
    $( Alternate definition of the class of all Tarski's classes. $)
    taralt $p |- Tarski = { y | ( A. z e. y ( ~P z C_ y
      /\ ~P z e. y ) /\ A. z e. ~P y ( z ~~ y \/ z e. y ) ) } $=
      ( vw ctarski cv cpw wss wrex wa wral cen wbr wcel wo cab df-tsk hbra1 wi
      wceq pweq sseq1d cbvralv r19.29 ssel visset pwex elpw syl5ibr imp adantl
      r19.23aiva syl ex sylbi adantr ralimdaa imdistani r19.26 3imtr4i anim1i
      simprl ssid sseq2 rcla4ev mpan2 ad2antll jca ralimiaa impbii abbii eqtri
      ) DBEZFZAEZGZVMCEZGZCVNHZIZBVNJZVLVNKLVLVNMZNBVNFJZIZAOVOVMVNMZIZBVNJZWBI
      ZAOABCPWCWGAWCWGVTWFWBVOBVNJZVRBVNJZIWHWDBVNJZIVTWFWHWIWJWHVRWDBVNVOBVNQW
      HVRWDRZWAWHVPFZVNGZCVNJZWKVOWMBCVNVLVPSVMWLVNVLVPTUAUBWNVRWDWNVRIWMVQIZCV
      NHWDWMVQCVNUCWOWDCVNWOWDVPVNMWMVQWDWMVMWLMWDVQWLVNVMUDVMVPVLBUEUFUGUHUIUJ
      UKULUMUNUOUPUQVOVRBVNURVOWDBVNURUSUTWFVTWBWEVSBVNWAWEIVOVRWAVOWDVAWDVRWAV
      OWDVMVMGZVRVMVBVQWPCVMVNVPVMVMVCVDVEVFVGVHUTVIVJVK $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T w y z $.
    tarval.1 $e |- T e. _V $.
    $( Properties of a Tarski's class. $)
    tarval $p |- ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ E. w e. T ~P z C_ w ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) $=
      ( vy cv cpw wss wrex wa wral cen wbr wcel wo ctarski wceq sseq2 rexeq
      anbi12d raleqbi1dv pweq breq2 eleq2 orbi12d raleqbidv df-tsk elab2 ) AFZG
      ZEFZHZUJBFHZBUKIZJZAUKKZUIUKLMZUIUKNZOZAUKGZKZJUJCHZUMBCIZJZACKZUICLMZUIC
      NZOZACGZKZJECPDUKCQZUPVEVAVJUOVDAUKCVKULVBUNVCUKCUJRUMBUKCSTUAVKUSVHAUTVI
      UKCUBVKUQVFURVGUKCUILUCUKCUIUDUEUFTEABUGUH $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T w y z $.
    $( Properties of a Tarski's class. $)
    tarvalg $p |- ( T e. A -> ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ E. w e. T ~P z C_ w ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) ) $=
      ( vy cv ctarski wcel cpw wss wrex wa wral cen wbr wo wb wceq eleq1 eleq2
      sseq2 rexeq anbi12d imbi12d ralbidv2 pweq eleq2d breq2 orbi12d bibi12d
      visset tarval vtoclg ) EFZGHZAFZIZUNJZUQBFJZBUNKZLZAUNMZUPUNNOZUPUNHZPZAU
      NIZMZLZQDGHZUQDJZUSBDKZLZADMZUPDNOZUPDHZPZADIZMZLZQEDCUNDRZUOVIVHVSUNDGSV
      TVBVMVGVRVTVAVLAUNDVTVDVOVAVLUNDUPTZVTURVJUTVKUNDUQUAUSBUNDUBUCUDUEVTVEVP
      AVFVQVTUPVFHUPVQHVEVPVTVFVQUPUNDUFUGVTVCVNVDVOUNDUPNUHWAUIUDUEUCUJABUNEUK
      ULUM $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T y z $.
    tarval1.1 $e |- T e. _V $.
    $( Properties of a Tarski's class. $)
    tarval1 $p |- ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ ~P z e. T ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) $=
      ( vy ctarski wcel cv cpw wss wa wral cen wbr wo cab taralt eleq2i ax-17
      wceq eleq2 sseq2 anbi12d imbi12d ralbidv2 pweq eleq2d breq2 orbi12d
      elabf bitri ) BEFBAGZHZDGZIZULUMFZJZAUMKZUKUMLMZUKUMFZNZAUMHZKZJZDOZFULBI
      ZULBFZJZABKZUKBLMZUKBFZNZABHZKZJZEVDBDAPQVCVNDBVNDRCUMBSZUQVHVBVMVOUPVGAU
      MBVOUSVJUPVGUMBUKTZVOUNVEUOVFUMBULUAUMBULTUBUCUDVOUTVKAVAVLVOUKVAFUKVLFUT
      VKVOVAVLUKUMBUEUFVOURVIUSVJUMBUKLUGVPUHUCUDUBUIUJ $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T y z $.
    $( Properties of a Tarski's class. $)
    tarval1g $p |- ( T e. A -> ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ ~P z e. T ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) ) $=
      ( vy cv ctarski wcel cpw wss wa wral cen wbr wo eleq1 wceq sseq2 eleq2
      anbi12d raleqbi1dv pweq breq2 orbi12d raleqbidv visset tarval1 vtoclbg )
      DEZFGAEZHZUHIZUJUHGZJZAUHKZUIUHLMZUIUHGZNZAUHHZKZJCFGUJCIZUJCGZJZACKZUICL
      MZUICGZNZACHZKZJDCBUHCFOUHCPZUNVCUSVHUMVBAUHCVIUKUTULVAUHCUJQUHCUJRSTVIUQ
      VFAURVGUHCUAVIUOVDUPVEUHCUILUBUHCUIRUCUDSAUHDUEUFUG $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x $.  $d T x y $.
    $( 1st axiom of a a Tarski's class.  The subsets of an element of a
       Tarski's class belong to the class. $)
    tarax1 $p |- ( ( T e. Tarski /\ A e. T ) -> ~P A C_ T ) $=
      ( vx vy wcel ctarski cpw wss cv wi wceq pweq sseq1d imbi2d wral wrex wa
      cen wbr wo tarvalg pm3.26 ralimi adantr syl6bi pm2.43i r19.21be vtoclri
      impcom ) ABEBFEZAGZBHZUJCIZGZBHZJUJULJCABUMAKZUOULUJUPUNUKBUMALMNUJUOCBUJ
      UOCBOZUJUJUOUNDIHDBPZQZCBOZUMBRSUMBETCBGOZQUQCDFBUAUTUQVAUSUOCBUOURUBUCUD
      UEUFUGUHUI $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x $.  $d T x $.
    $( 2nd axiom of a a Tarski's class.  The powerset of an element of a
       Tarski's class belongs to the class. $)
    tarax2 $p |- ( ( T e. Tarski /\ A e. T ) -> ~P A e. T ) $=
      ( vx wcel ctarski cpw cv wi wceq pweq eleq1d imbi2d wral wss wa cen wbr
      wo tarval1g pm3.27 ralimi adantr syl6bi pm2.43i r19.21be vtoclri impcom
      ) ABDBEDZAFZBDZUHCGZFZBDZHUHUJHCABUKAIZUMUJUHUNULUIBUKAJKLUHUMCBUHUMCBMZU
      HUHULBNZUMOZCBMZUKBPQUKBDRCBFMZOUOCEBSURUOUSUQUMCBUPUMTUAUBUCUDUEUFUG $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x $.  $d T x y $.
    $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class is either
       equipotent to the class or an element of the class. $)
    tarax3 $p |- ( ( T e. Tarski /\ A e. ~P T ) ->
      ( A ~~ T \/ A e. T ) ) $=
      ( vx vy cpw wcel ctarski cen wbr wo cv wi wceq breq1 eleq1 orbi12d
      imbi2d wral wss wrex wa tarvalg pm3.27 syl6bi pm2.43i r19.21be vtoclri
      impcom ) ABEZFBGFZABHIZABFZJZUJCKZBHIZUNBFZJZLUJUMLCAUIUNAMZUQUMUJURUOUKU
      PULUNABHNUNABOPQUJUQCUIUJUQCUIRZUJUJUNEZBSUTDKSDBTUACBRZUSUAUSCDGBUBVAUSU
      CUDUEUFUGUH $.
      $( [30-Dec-2010] $)
  $}

  $( The empty set is an inaccessible ardinal. $)
  empistar $p |- (/) e. Tarski $=
    ( vx c0 cvv wcel ctarski 0ex cv cpw wss wa wral cen wbr wo csn pw0 eleq2i
    wceq elsni enref breq1 mpbiri orcd syl sylbi rgen a1i ral0 jctil tarval1g
    mpbird ax-mp ) BCDZBEDZFUMUNAGZHZBIUPBDJZABKZUOBLMZUOBDZNZABHZKZJUMVCURVCUM
    VAAVBUOVBDUOBOZDZVAVBVDUOPQVEUOBRZVAUOBSVFUSUTVFUSBBLMBFTUOBBLUAUBUCUDUEUFU
    GUQAUHUIACBUJUKUL $.
    $( [30-Dec-2010] $)

  $( The parts of an element of a Tarski's class are Tarski's classes. $)
  elincin $p |- ( ( T e. Tarski /\ A e. T /\ B C_ A ) -> B e. T ) $=
    ( cvv wcel ctarski wss w3a ssexg ancoms 3adant1 wi cpw elpwg biimprcd
    3ad2ant3 wa tarax1 ssel2 a1d ex syl 3adant3 syld com13 pm2.43i mpcom ) BDEZ
    CFEZACEZBAGZHZBCEZUJUKUHUIUKUJUHBACIJKUHULUMLULUHUHUMULUHBAMZEZUHUMLZUKUIUH
    UOLUJUHUOUKBADNOPUIUJUOUPLZUKUIUJQUNCGZUQACRURUOUPURUOQUMUHUNCBSTUAUBUCUDUE
    UFUG $.
    $( [30-Dec-2010] $)

  $( The intersection of two elements of a Tarski's class belongs to the
     class. $)
  inacint $p |- ( ( T e. Tarski /\ A e. T /\ B e. T ) -> ( A i^i B ) e. T ) $=
    ( ctarski wcel w3a cpw cin wss tarax1 3adant3 ssid elpwg mpbiri 3ad2ant2
    inpws1 syl sseldd ) CDEZACEZBCEZFZAGZCABHZSTUCCIUAACJKUBAUCEZUDUCETSUEUATUE
    AAIALAACMNOABAPQR $.
    $( [30-Dec-2010] $)

  $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class is either
     equipotent to the class or an element of the class. $)
  tarax3a $p |- ( ( T e. Tarski /\ A C_ T ) ->
      ( A ~~ T \/ A e. T ) ) $=
    ( ctarski wcel wss wa cen wbr wo wi cvv cpw elpwg tarax3 expcom syl6bir
    com13 imp ssexg syl5com ancoms pm2.43i ) BCDZABEZFZABGHABDIZUDUCUEUFJUEAKDZ
    UFUDUCFUCUDUGUFJUGUDUCUFUGUDABLDZUCUFJABKMUCUHUFABNOPQRABCSTUAUB $.
    $( [17-Apr-2011] $)

  $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class non
     equipotent to the class is an element of the class. $)
  tarax3b $p |- ( ( T e. Tarski /\ A e. ~P T /\ -. A ~~ T ) ->
       A e. T ) $=
    ( ctarski wcel cpw cen wbr wn wa tarax3 ord 3impia ) BCDZABEDZABFGZHABDZMNI
    OPABJKL $.
    $( [22-Feb-2011] $)

  $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class non
     equipotent to the class is an element of the class. $)
  tarax3c $p |- ( ( T e. Tarski /\ A C_ T /\ -. A ~~ T ) ->
       A e. T ) $=
    ( ctarski wcel wss cen wbr wn w3a cpw cvv wb ssexg ancoms 3adant3 elpwg
    bicomd 3anbi2d syl tarax3b syl6bi pm2.43i ) BCDZABEZABFGHZIZABDZUFUFUCABJDZ
    UEIZUGUFAKDZUFUILUCUDUJUEUDUCUJABCMNOUJUDUHUCUEUJUHUDABKPQRSABTUAUB $.
    $( [22-Feb-2011] $)

  $( A element of a Tarski's class is strictly dominated by the class.  JFM
     CLASSES2 th. 1 $)
  tarax3d2 $p |- ( ( T e. Tarski /\ A e. T ) -> A ~< T ) $=
    ( ctarski wcel cpw csdm wbr cdom wa sdomdomtr pm3.26 canth2g adantl cen wo
    wss tarax1 wi cvv pwexg ssdomg imp brdom2 biimpi a1i12 com13 mpcom ex
    com23 syl anabsi7 mpd sylibr jca sylc ) BCDZAAEZFGZUQBHGZIABFGUPABDZIZAUQBC
    JUPUTKVAURUSUTURUPABLMVAUQBFGUQBNGOZUSVAUQBPZVBABQUPUTVCVBRZUTUQSDZVAVDRABT
    VEVCVAVBVEVCVAVBRZUSVEVCIZVFVEVCUSUQBSUAUBVAVGUSVBVAVGUSVBRUSVBUQBUCZUDUEUF
    UGUHUIUJUKULVHUMUNUO $.
    $( [22-Feb-2011] $)

  $( The cardinal of an element of a Tarski's class belongs to the cardinal of
     the Tarski's class.  JFM CLASSES2 th. 1 $)
  tarax3d3 $p |- ( ( T e. Tarski /\ A e. T )
    -> ( card ` A ) e. ( card ` T ) ) $=
    ( ctarski wcel wa ccrd cfv csdm wbr tarax3d2 wb cardsdom ancoms mpbird ) BC
    DZABDZEAFGBFGDZABHIZABJPOQRKABBCLMN $.
    $( [20-Apr-2011] $)


  $( The cardinal of an element of a Tarski's class is strictly less than the
     cardinal of the Tarski's class.  JFM CLASSES2 th. 1 $)
  tarax3d4 $p |- ( ( T e. Tarski /\ A e. T )
    -> ( card ` A ) ~< ( card ` T ) ) $=
    ( ctarski wcel wa ccrd cfv csdm wbr tarax3d3 wceq cardidm a1i eleq12d
    mpbird cvv wb fvex pm3.2i cardsdom syl mpbid ) BCDABDEZAFGZFGZBFGZFGZDZUDUF
    HIZUCUHUDUFDABJUCUEUDUGUFUEUDKUCALMUGUFKUCBLMNOUCUDPDZUFPDZEZUHUIQULUCUJUKA
    FRBFRSMUDUFPPTUAUB $.
    $( [20-Apr-2011] $)

  $( A part of a Tarski's class strictly dominated by the class is an element
     of the class.  JFM CLASSES2 th. 2. $)
  tarax3e $p |- ( ( T e. Tarski /\ A e. ~P T /\ A ~< T ) -> A e. T ) $=
    ( ctarski wcel cpw csdm wbr wa cdom cen wn wi brsdom wo tarax3 adantl ord
    ex com23 imp sylbi com12 3impia ) BCDZABEDZABFGZABDZUFUDUEHZUGUFABIGZABJGZK
    ZHUHUGLZABMUIUKULUIUHUKUGUIUHUKUGLUIUHHUJUGUHUJUGNUIABOPQRSTUAUBUC $.
    $( [22-Feb-2011] $)

  $( A part of a Tarski's class strictly dominated by the class is an element
     of the class.  JFM CLASSES2 th. 2. $)
  tarax3f $p |- ( ( T e. Tarski /\ A C_ T /\ A ~< T ) -> A e. T ) $=
    ( ctarski wcel wss csdm wbr cvv wi relsdom brrelexi cpw elpwg tarax3e 3exp
    com3l syl6bir com23 mpcom com13 3imp ) BCDZABEZABFGZABDZUDUCUBUEAHDZUDUCUBU
    EIZIABFJKUFUCUDUGUFUCABLDZUDUGIABHMUBUHUDUEUBUHUDUEABNOPQRSTUA $.
    $( [17-Apr-2011] $)


  $( A singleton of an element of a Tarski's class belongs to the class.  JFM
     CLASSES2 th. 2 (partly). $)
  tarsin $p |- ( ( T e. Tarski /\ A e. T ) -> { A } e. T ) $=
    ( ctarski wcel wa cpw csn wss tarax2 tarax1 syldan snsspw cvv wb snex a1i
    elpwg syl mpbiri sseldd ) BCDZABDZEZAFZFZBAGZUAUBUDBDUEBHABIUDBJKUCUFUEDZUF
    UDHZALUCUFMDZUGUHNUIUCAOPUFUDMQRST $.
    $( [22-Feb-2011] $)

  ${
    $d T x $.
    $( A non empty Tarski's class contains the empty set. $)
    emptar $p |- ( ( T e. Tarski /\ T =/= (/) ) -> (/) e. T ) $=
      ( vx cv wcel wex ctarski c0 wne wa n0 biimpi adantl wi cpw wss tarax1
      0elpw ssel mpi syl ex adantr com12 19.23aiv mpcom ) BCZADZBEZAFDZAGHZIZGA
      DZUJUHUIUJUHBAJKLUGUKULMBUKUGULUIUGULMUJUIUGULUIUGIUFNZAOZULUFAPUNGUMDULU
      FQUMAGRSTUAUBUCUDUE $.
      $( [30-Dec-2010] $)
  $}

  $( One is an element of a non empty Tarski's class. $)
  tarone $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 1o e. T ) $=
    ( ctarski wcel c0 wne wa csn c1o emptar tarsin syldan df1o2 syl5eqel ) ABCZ
    ADEZFDGZAHNODACPACAIDAJKLM $.
    $( [22-Feb-2011] $)


  $( Two is an element of a non empty Tarski's class. $)
  tartwo $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 2o e. T ) $=
    ( ctarski wcel c0 wne wa csn cpr c2o c1o tarone wi df1o2 eleq1i cpw tarax2
    pwpw0 syl5eqelr ex adantr com12 sylbi mpcom df2o2 syl5eqel ) ABCZADEZFZDDGZ
    HZAIJACZUHUJACZAKUKUIACZUHULLJUIAMNUHUMULUFUMULLUGUFUMULUFUMFUIOAUJUIAPQRST
    UAUBUCUDUE $.
    $( [22-Feb-2011] $)

  $( If a Tarski's class is not empty it has more than two elements. $)
  tarmrtwo $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 2o ~< T ) $=
    ( ctarski wcel c0 wne c2o csdm wbr tartwo tarax3d2 syldan ) ABCADEFACFAGHAI
    FAJK $.
    $( [22-Feb-2011] $)

  $( If ` A ` and ` B ` are members of a Tarski's class, their unordered pair
     is also an element of the class.  JFM CLASSES2 th. 3 (partly). $)
  tarunpa $p |- ( ( T e. Tarski /\ A e. T /\ B e. T )
    -> { A , B } e. T ) $=
    ( ctarski wcel cpr cpw csdm wbr w3a tarax3e 3simp1 wss wa prssg ibi
    3adant1 cvv wb prex a1i elpwg syl mpbird c2o cdom domsdomtr unpam2 c0 wne
    tarmrtwo ne0i 3ad2ant2 sylanc syl3anc ) CDEZABFZCGEZUQCHIZUQCEUPACEZBCEZJZU
    QCKUPUTVALZVBURUQCMZUTVAVDUPUTVANVDABCCCOPQVBUQREZURVDSVEVBABTUAUQCRUBUCUDU
    QUEUFIZUECHIZUSVBUQUECUGUTVAVFUPABCCUHQUPCUIUJZVGVBCUKVCUTUPVHVACAULUMUNUNU
    O $.
    $( [22-Feb-2011] $)

  $( If ` A ` and ` B ` are members of a Tarski's class, their ordered pair is
     also an element of the class.  JFM CLASSES2 th. 4. $)
  tarorpa $p |- ( ( T e. Tarski /\ A e. T /\ B e. T )
    -> <. A , B >. e. T ) $=
    ( ctarski wcel w3a csn cpr cop tarunpa 3simp1 tarsin 3adant3 syl3anc df-op
    syl5eqel ) CDEZACEZBCEZFZAGZABHZHZCABIQUACEZUBCEUCCETUAUBCJQRSKQRUDSACLMABC
    JNABOP $.
    $( [22-Feb-2011] $)

  ${
    $d A x y z $.  $d B x y z $.  $d T x y z $.
    $( A cross product of two parts of a Tarski's class is a part of the
       class. $)
    tarcrpr $p |- ( ( T e. Tarski /\ A C_ T /\ B C_ T )
    -> ( A X. B ) C_ T ) $=
      ( vz vx vy ctarski wcel wss w3a cv cxp wral cop wa wi ssel com12
      im2anan9 tarorpa 3exp com3l imp syl6 ex com3r 3imp r19.21aivv eleq1
      ralxp sylibr dfss3 ) CGHZACIZBCIZJZDKZCHZDABLZMZUSCIUPEKZFKZNZCHZFBMEAMUT
      UPVDEFABUMUNUOVAAHZVBBHZOZVDPZUNUOUMVHUNUOUMVHPVGUNUOOZUMVDVGVIVACHZVBCHZ
      OUMVDPZVEUNVJVFUOVKUNVEVJACVAQRUOVFVKBCVBQRSVJVKVLUMVJVKVDUMVJVKVDVAVBCTU
      AUBUCUDUBUEUFUGUHURVDDEFABUQVCCUIUJUKDUSCULUK $.
      $( [22-Feb-2011] $)
  $}

  $( An element of a transitive Tarski's class is a part of the class.  JFM
     CLASSES2 th. 48 $)
  elcartr $p |- ( ( T e. Tarski /\ Tr T /\ A e. T ) -> A C_ T ) $=
    ( wtr wcel wss ctarski trss imp 3adant1 ) BCZABDZABEZBFDJKLBAGHI $.
    $( [30-Dec-2010] $)

  $( A transitive element of a Tarski's class is a part of the class.  JFM
     CLASSES2 th. 8 $)
  tartrel $p |- ( ( T e. Tarski /\ Tr A /\ A e. T ) -> A C_ T ) $=
    ( ctarski wcel wtr wss wi cpw dftr4 wa sstr2 tarax1 syl5com ex com3r sylbi
    com12 3imp ) BCDZAEZABDZABFZTSUAUBGZTAAHZFZSUCGAISUAUEUBSUAUEUBGUEUDBFUBSUA
    JAUDBKABLMNOPQR $.
    $( [22-Feb-2011] $)

  $( If an element of a Tarski's class is an ordinal number, it is a part of
     the class.  JFM CLASSES2 th. 6 (partly) $)
  tartord $p |- ( ( T e. Tarski /\ Ord A /\ A e. T ) -> A C_ T ) $=
    ( ctarski wcel word wss wi wtr cep wwe wa df-ord tartrel 3exp com12 adantr
    sylbi 3imp ) BCDZAEZABDZABFZTSUAUBGZTAHZAIJZKSUCGZALUDUFUESUDUCSUDUAUBABMNO
    PQOR $.
    $( [22-Feb-2011] $)

  ${
    $d T t $.  $d t x $.
    $( A non empty Tarski class is infinite. $)
    tclinf $p |- ( ( T e. Tarski /\ T =/= (/) ) -> om ~<_ T ) $=
      ( vt vx ctarski wcel c0 wne com cdom wbr cv wa wi wceq eleq1 neeq1
      anbi12d breq2 imbi12d cuni wss visset dominf pm3.27 wal cpw elunii pwid
      tarax2 sylancr ex adantr 19.21aiv dfss2 sylibr sylanc vtoclg anabsi5 ) AD
      EZAFGZHAIJZBKZDEZVBFGZLZHVBIJZMUSUTLZVAMBADVBANZVEVGVFVAVHVCUSVDUTVBADOVB
      AFPQVBAHIRSVDVBVBTZUAZVFVEVBBUBUCVCVDUDVECKZVBEZVKVIEZMZCUEVJVEVNCVCVNVDV
      CVLVMVKVKUFZEVOVBEVMVCVLLVKVOVBUGVKCUBUHVKVBUIUJUKULUMCVBVIUNUOUPUQUR $.
      $( [22-Feb-2011] $)
  $}

  ${
    $d A a b $.  $d B b $.  $d T a b $.
    $( The cross product of two elements of a transitive Tarski's class is an
       element of the class.  JFM CLASSES2 th. 67 (partly). $)
    cptarc $p |- ( ( T e. Tarski /\ Tr T /\ ( A e. T /\ B e. T ) )
    -> ( A X. B ) e. T ) $=
      ( va vb ctarski wcel wtr wa cxp cv wi wceq xpeq1 eleq1d imbi2d xpeq2 c0
      wne ne0i w3a xp0 eqtr mpan2 eleq1 emptar ancoms syl5cbir 3adant2 adantr
      com12 3syl com cdom wbr wn df-ne cen csdm tarax3d2 expcom imp 3adant1
      wss elcartr tarcrpr ensdomtr ex tarax3f 3exp 3ad2ant3 com3r syl6 com24
      com4l syl com14 com4r anabsi5 com13 3adant3l syl5com 3adant3r pm2.43i
      mpd visset infxpabs sylbir cvv wb domtri2 mp2an domtr xp0r syl5eqel
      syl5bir a1i12 3simpl3 3ad2ant2 3simp1 pm3.27 adantl ad2antrr sylanc
      syl3anc com3l entr xpcomen sylancr pm2.61ine a1dd sdomdom sylc sylbi
      pm2.61i omex 3jca sdomdomtr breq1 0sdomg biimpar a1i sylancl anim2i a1d
      syl5 ax-mp 3imp1 3exp1 cptwff tclinf mpan2d impcom vtocl2ga 3imp ) CFGZCH
      ZACGBCGIZABJZCGZYRYPYQYTYPYQDKZEKZJZCGZLZLZYPYQAUUBJZCGZLZLYPYQYTLZLDEABC
      CUUAAMZUUEUUIYPUUKUUDUUHYQUUKUUCUUGCUUAAUUBNOPPUUBBMZUUIUUJYPUULUUHYTYQUU
      LUUGYSCUUBBAQOPPUUACGZUUBCGZUUFUUMCRSZUUMUUNIZUUFLCUUATUUOUUPYPYQUUDUUBRM
      ZUUOUUPYPUAZYQIZUUDLZUUQUUCUUARJZMZUUCRMZUUTUUBRUUAQUVBUVARMUVCUUAUBUUCUV
      ARUCUDUUSUVCUUDUURUVCUUDLZYQUUOYPUVDUUPUVCUUDRCGZUUOYPIZUUCRCUEYPUUOUVECU
      FUGZUHUIUJUKULUMUUAUNUOZUUQUPZUUTLZUUBUUAUNUOZUVHUVJLZUVIUVHUVKUUTUVIUUBR
      SZUVHUVKUUTLZLUUBRUQZUVHUVMUVNUVHUVMUVKUUTUUSUUCUUAURUOZUUDUVHUVMUVKUAUUS
      UUACUSUOZUVPUUDLZUURUVQYQUUPYPUVQUUOUUPYPUVQUUMYPUVQLUUNYPUUMUVQUUACUTVAU
      JVBVCUJUUSUVQUVRLZUURYQUUSUVSLZUUPYPYQUVTLZUUOUUPYPUWAYPYQUUPUVTYPYQUUPUV
      TYPYQUUPUAZUVTYPYQUUMUWBUVTLUUNUWBUUACVDZUVTYPYQUUMUAZYPYQUUNUWCUVTLZUUMY
      PYQUUNUAZUUBCVDZUWEUUBCVEZUUSUWCUWGUVSUURYQUWCUWGUVSLLZYPUUOUUSUWILUUPUUS
      UWCUWGYPUVSYPUWCUWGUUSUVSYPUWCUWGUVTYPUWCUWGUAZUUCCVDZUVTUUAUUBCVFZUVPUWK
      UUSUVQUUDUVPUVQUUSUWKUUDUVPUVQUUCCUSUOZUUSUWKUUDLLUVPUVQUWMUUCUUACVGVHUUS
      UWKUWMUUDUURUWKUWMUUDLLZYQYPUUOUWNUUPYPUWKUWMUUDUUCCVIZVJVKUJVLVMVNVOVPVJ
      VQVRVKVSVTVPWAUUACVEZWBWCWDVJVLVBVCVBWDWEUUAUUBDWFZEWFZWGWBVJUKWHVTUVKUPZ
      UUAUUBUSUOZUVLUUBWIGZUUAWIGZUWSUWTWJUWRUWQUUBUUAWIWIWKWLUUAUUBUNUOZUXCUVL
      UWTUVHUXCUXCUVJUVHUXCUMUUBUNUOZUXCUVJLUVHUXCUXDUMUUAUUBWMVHUXDUXCUUTUVIUX
      DUXCUUTLZLUUARUXDUXCUUSUUARMZUUDUXDUXCUUSUXFUUDLZLUURUXGYQUUOYPUXGUUPUXFU
      VFUUDUXFUUCRUUBJZMZUVFUUDLUUARUUBNZUXIUUDUXHCGUVFUUCUXHCUEUVFRCUXHUVGUUBW
      NZWOWPVPUKUIUJWQVRUXDUUARSZUXEUXDUXLUXCUUTUUCUUBUUAJZURUOZUXMUUBURUOZUUTU
      XDUXLUXCUAZUUSUUCUUBURUOZUUDUXNUXOIZUUSUXQUUDLZUURYQUUSUXSLZUUPYPYQUXTLZU
      UOUUPYPUYAYPYQUUPUXTYPYQUUPUXTUWBUXTYPYQUUMUWBUXTLUUNUWBUWCUXTUWDYPYQUUNU
      WCUXTLZUUMUWFUWGUYBUWHUUSUWGUWCUXSUUSUWGUWCUXSLLUUSUWCUWGUUSUXSUUSYPUWCUW
      GUXTLLUUOUUPYPYQWRZYPUWCUWGUXTUWJUWKUXTUWLUWKUUSUXQUUDYPUWKUWMUUDUWKUUSUX
      QUAUWOUUSUWKYPUXQUYCWSUWKUUSUXQWTUUSUXQUWMUWKUXQUUBCUSUOZUWMUUSUXQIUUCUUB
      CVGZUUSUXQXAUURUYDYQUXQUUPYPUYDUUOUUPYPUYDUUNYPUYDLUUMYPUUNUYDUUBCUTVAXBV
      BVCZXCXDVCXEVJVPVJVPVNWDXFVPWAUWPWBWCWDVJVLVBVCVBWDUUCUXMUUBXGZWBUUAUUBUW
      QUWRXHZUUBUUAUWRUWQWGZXIVJUKXJXKVMVTUUAUUBXLZUYJXMXNXOUVHUPZUUAUMUSUOZUVJ
      UMWIGZUXBUYKUYLWJXPUWQUMUUAWIWIWKWLUXDUYLUVJLZUXDUYLUVIUUSUUDYPUWKUWMUUDU
      XDUYLUVIUAZUUSIZUWOUUSYPUYOUYCXBUYPUWJUWKUUSUWJUYOUUSYPUWCUWGUYCUURYQUWCU
      UPYPYQUWCLZUUOUUPYPUYQUUMYPUYQLUUNYPYQUUMUWCYPYQUUMUWCUWPVJVLUJVBVCVBZUUR
      YQUWGUUPYPYQUWGLZUUOUUPYPUYSUUNYPUYSLUUMYPYQUUNUWGYPYQUUNUWGUWHVJVLXBVBVC
      VBZXQXBUWLVPUXDUYLUVIUUSUWMUVIUXDUYLUUSUWMLZUVIUVMUXDUYLVUALLUVOUYLUXDUVM
      VUAUYLUXDUVMVUALZUYLUXDIZVUBUXAVUCVUCVUBLZLUWRUXAVUCUWTVUDUUAUMUUBWIXRUWT
      VUDLZUUARUXIUXHRMZVUEUXFUXIVUFIUVCVUEUUCUXHRUCUWTVUCUVMUVCVUAUWTVUCUVMUVC
      VUALZLVUGUVMUUSUVCUWMUURUVCUWMLZYQUUOYPVUHUUPYPUUOVUHUVCUWMRCUSUOZYPUUOIZ
      UUCRCUSXSYPVUIUUOCFXTYAUHUGUIUJUKYBWQVRVPUXJUXKYCUXLUXCVUDUWTVUCUXLUXCVUB
      UXDUXLUXCVUBLLUYLUXDUXLUXCVUBUXNUXOVUBUXPUXRUXQVUBUYGUXQVUAUVMUXQUUSUXQUY
      DIZUWMUXQUUSVUKUUSUYDUXQUURUYDYQUYFUJYDVHUYEVMYEVPUYHUYIXIVJXBXFUYJYFXJVM
      YGWDVHVTWHXFYHXEYIUXDUPZUUBUMUSUOZUYNUYMUXAVULVUMWJXPUWRUMUUBWIWIWKWLUYLV
      UMUVJUYLVUMIUUCUMUSUOZUVJUUAUUBYJVUNUVIUUSUUDYPUWKUWMUUDVUNUVIUUSUAZUWOUU
      SVUNYPUVIUYCVKZYPUWCUWGUWKVUOUWLVUPUUSVUNUWCUVIUYRVKUUSVUNUWGUVIUYTVKXEVU
      NUUSUWMUVIUUSVUNUWMUURVUNUWMLZYQUUOYPVUQUUPYPUUOVUQVUJVUNUMCUNUOZUWMCYKYP
      VUNVURIUWMLUUOUUCUMCFXRUJYLUGUIUJYMUIXEVJVPVAXNXOXNXOXOYIVPVSYNXFYO $.
      $( [15-Apr-2011] $)
  $}

  $( Set exponentiation is an element of a transitive Tarski's class.  JFM
     CLASSES2 th. 67 (partly). $)
  sexptrt $p |- ( ( T e. Tarski /\ Tr T /\ ( A e. T /\ B e. T ) )
    -> ( A ^m B ) e. T ) $=
    ( ctarski wcel wtr wa w3a cm co cxp wi pm3.22 3anim3i cptarc cpw wss
    elincin 3exp com23 3ad2ant1 mapsspw syl5com adantl 3ad2ant3 pm2.43i tarax2
    ex com12 3syl ) CDEZCFZACEZBCEZGZHZABIJZCEZUPUKULUNUMGZHBAKZCEZUPURLZUOUSUK
    ULUMUNMNBACOUPVAURUPVAURLZUKULUPVCLUOUKVAUPURUKVAVBUPUTPZCEZURUKVAGUPVEURLZ
    UOUKUPVFLZULUNVGUMUPUQVDQZVFUNUKULVHVFLUOUKVEVHURUKVEVHURVDUQCRSTUAABCUBUCU
    DUEUFUTCUGUCUHTUAUFUIUJUF $.
    $( [15-Apr-2011] $)


  $( If an element of a Tarski's class is an ordinal number, its successor is a
     part of the class.  JFM CLASSES2 th. 6 (partly) $)
  tarsuc1 $p |- ( ( T e. Tarski /\ Ord A /\ A e. T ) -> suc A C_ T ) $=
    ( ctarski wcel word w3a csn cun csuc wss wa tartord snssi 3ad2ant3 jca
    unss sylib df-suc syl5ss ) BCDZAEZABDZFZAAGZHZBAIUCABJZUDBJZKUEBJUCUFUGABLU
    BTUGUAABMNOAUDBPQARS $.
    $( [22-Feb-2011] $)

  $( If an element of a Tarski's class is an ordinal number, its successor is
     an element of the class.  JFM CLASSES2 th. 6 (partly). $)
  tarsuc2 $p |- ( ( T e. Tarski /\ Ord A /\ A e. T ) -> suc A e. T ) $=
    ( ctarski wcel word w3a csuc wss tarsuc1 cen wbr wn wi csdm csn cun com
    cdom 3simp3 c0 wne wa cvv snex jctir cxp infxpg ensymg entr ex unexg
    sylancl ensdomtr syl6 syl com3l com24 com23 mpcom xpsneng syl5com anidms
    com13 3expia com12 tarax3d2 3adant2 pm2.43i snnzg omex domtri notbid
    notnot2 cfn unfi isfinite tclinf sdomdomtr exp3a adantr mpid 3ad2ant1 ne0i
    3ad2ant3 sylbir isfinite2 snfi syl6bi mpan pm2.61d df-suc syl5eqbr sdomnen
    tarax3c 3exp mpd ) BCDZAEZABDZFZAGZBHZXABDZABIWTXABJKLZXBXCMZWTXABNKXDWTAAO
    ZPZBXANWTQARKZXGBNKZWSWTXHXIMWQWRWSSZWSXHWTXIXHXFTUAZWTXIMZWSXHXKXLWTXHXKUB
    ZXIWTXMXIMZWQWSWTXNMWRWTABNKZXNWQWSUBWSXFUCDZUBZWTXOXNMWTWSXPXJAUDZUEXQXMXO
    WTXIXMXQXOXLMZXHXKXQXSXHXKXQFAXFUFZXGJKZXSAXFBUCUGWTXOYAXIWSWTXOYAXIMMZXJWS
    WTYBMZWTXTAJKZYBWSWSUBWSWTYDYBMXJWSYDWTYBWSYDAXTJKZYCXTABUHYEYAXOWTXIYEYAAX
    GJKZXSYEYAYFAXTXGUIUJWTYFXOXIWTXGUCDZYFXOXIMZMWSXPYGWTAXFBUCUKXJXRULYGYFXGA
    JKZYHAXGUCUHYIXOXIXGABUMUJUNUOUPUNUQUNURUSAABBUTVAVBUSVCUOVDVEUQUSABVFVAVGV
    HVEUJABVIVAURUSWTXHLZXIMZWSWQWTYKMWRWSYJWTXIQUCDZWSYJXLMVJYLWSUBZYJAQNKZLZL
    ZXLYMXHYOQAUCBVKVLYPYNXLYNVMAVNDZXFVNDZXLYNYQYRUBXGVNDZXLAXFVOYSXGQNKZXLXGV
    PWTYTXIWTYTXIMZWSWQWTUUAMWRWTBTUAZUUAWSWQWRUUBUUAMWSWQUUBUUAWQUUBUBYTQBRKZX
    IBVQWQYTUUCXIMMUUBWQYTUUCXIXGQBCVRVSVTWAUJWBBAWCVAWDVHVEWEUOAWFAWGULUOWHWIU
    RWDVHWJAWKWLXABWMUOWQWRXDXEMWSWQXBXDXCWQXBXDXCXABWNWOURWBWPWP $.
    $( [22-Feb-2011] $)

  ${
    $d A t x $.  $d t w x z $.
    $( Any set has a Tarski's class that contains it. $)
    settrcon $p |- ( A e. B -> E. t e. Tarski A e. t ) $=
      ( vx vz vw cv wcel ctarski wrex wceq eleq1 rexbidv wa wex cpw wss wral
      cen wbr wo w3a axgroth5 3anass bicomi exbii mpbir ancom visset tarval
      anbi2i bitri df-rex vtoclg ) DGZAGZHZAIJZBUPHZAIJDBCUOBKUQUSAIUOBUPLMURUP
      IHZUQNZAOZVBUQEGZPZUPQVDFGQFUPJNEUPRZVCUPSTVCUPHUAEUPPRZNZNZAOZVIUQVEVFUB
      ZAODAEFUCVHVJAVJVHUQVEVFUDUEUFUGVAVHAVAUQUTNVHUTUQUHUTVGUQEFUPAUIUJUKULUF
      UGUQAIUMUGUN $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d a b y $.
    $( A function that maps a set ` a ` to the smallest Tarski's class that
       contains the set. $)
    df-tskmp $a |- tarskiMap = { <. a , b >. | b = |^| { y | ( a e. y /\
      y e. Tarski ) } } $.
  $}

  ${
    $d A a y $.  $d A x y $.  $d a b y $.  $d w x y z $.
    tarval2.1 $e |- A e. _V $.
    $( Value of our tarski map. $)
    tarval2 $p |- ( tarskiMap ` A ) = |^| { y | ( A e. y /\
      y e. Tarski ) } $=
      ( vb va vx vz vw ctarskim cfv cv wcel ctarski wa cab cint wceq copab
      df-tskmp fveq1i c0 wne cvv wex ax-17 eleq1 anbi1d exbidv cpw wss wrex
      wral cen wbr wo w3a axgroth5 3anass bicomi exbii mpbir visset tarval
      anbi2i vtoclf abn0 intex mpbi abbid inteqd fvopab eqtri ) BIJBDKEKZAKZLZV
      NMLZNZAOZPZQEDRZJBVNLZVPNZAOZPZBIVTAEDSTEDBVSWDCWCUAUBZWDUCLWEWBAUDZFKZVN
      LZVPNZAUDZWFFBWFFUECWGBQZWIWBAWKWHWAVPWGBVNUFUGUHWJWHGKZUIZVNUJWMHKUJHVNU
      KNGVNULZWLVNUMUNWLVNLUOGVNUIULZNZNZAUDZWRWHWNWOUPZAUDFAGHUQWQWSAWSWQWHWNW
      OURUSUTVAWIWQAVPWPWHGHVNAVBVCVDUTVAVEWBAVFVAWCVGVHVMBQZVRWCWTVQWBAWTAUEWT
      VOWAVPVMBVNUFUGVIVJVKVL $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x y $.
    $( Value of our tarski map. $)
    tarval2g $p |- ( A e. B -> ( tarskiMap ` A ) = |^| { y | ( A e. y /\
      y e. Tarski ) } ) $=
      ( vx cv ctarskim cfv wcel ctarski wa cab cint wceq fveq2 eleq1 anbi1d
      abbidv inteqd eqeq12d visset tarval2 vtoclg ) DEZFGZUCAEZHZUEIHZJZAKZLZMB
      FGZBUEHZUGJZAKZLZMDBCUCBMZUDUKUJUOUCBFNUPUIUNUPUHUMAUPUFULUGUCBUEOPQRSAUC
      DTUAUB $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Being a part of ` ( tarskiMap `` A ) ` . $)
    bpmp $p |- ( ( A e. C /\ B e. D ) ->
      ( B C_ ( tarskiMap ` A ) <-> A. x e. Tarski ( A e. x ->
        B C_ x ) ) ) $=
      ( vy wcel wa ctarskim cfv wss cv ctarski cab cint wi wral wceq tarval2g
      adantr sseq2d wal wb pm2.27 ex com13 com3r imp com12 impbii albii eleq1
      eleq2 sseq2 imbi12d cbvalv bitri visset anbi12d elab imbi1i df-ral
      3bitr4i a1i syl5bb ssint bitrd ) BDGZCEGZHZCBIJZKCBALZGZVLMGZHZANZOZKZVMC
      VLKZPZAMQZVJVKVQCVHVKVQRVIABDSTUAVJCFLZKZFVPQZWAVRVJWBVPGZWCPZFUBZWAWDWGW
      AUCVJBWBGZWBMGZHZWCPZFUBZVNVTPZAUBZWGWAWLWIWHWCPZPZFUBWNWKWPFWKWPWHWIWKWC
      WHWIWKWCPWJWCUDUEUFWJWPWCWHWIWPWCPWIWPWHWCWIWOUDUGUHUIUJUKWPWMFAWBVLRZWIV
      NWOVTWBVLMULWQWHVMWCVSWBVLBUMWBVLCUNUOUOUPUQWFWKFWEWJWCVOWJAWBFURVLWBRVMW
      HVNWIVLWBBUMVLWBMULUSUTVAUKVTAMVBVCVDWCFVPVBVEFCVPVFVEVG $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C y $.  $d D y $.
    $( Belonging to ` ( tarskiMap `` A ) ` . $)
    btmp $p |- ( ( A e. C /\ B e. D ) ->
    ( B e. ( tarskiMap ` A ) <-> A. x e. Tarski ( A e. x -> B e. x ) ) ) $=
      ( vy wcel wa ctarskim cfv cv ctarski cab cint wral wi wceq tarval2g
      adantr eleq2d wb elintg adantl wal ax-17 pm2.27 ex com13 com3r imp com12
      impbii a1i albid df-ral syl6bbr eleq2 imbi12d cbvralv syl6bb cvv visset
      eleq1 anbi12d elabg ax-mp imbi1i albii syl5bb 3bitrd ) BDGZCEGZHZCBIJZGCB
      AKZGZVOLGZHZAMZNZGZCFKZGZFVSOZVPCVOGZPZALOZVMVNVTCVKVNVTQVLABDRSTVLWAWDUA
      VKFCVSEUBUCVMWBVSGZWCPZFUDZWGWDVMBWBGZWBLGZHZWCPZFUDZWGWJVMWOWKWCPZFLOZWG
      VMWOWLWPPZFUDWQVMWNWRFVMFUEWNWRUAVMWNWRWKWLWNWCWKWLWNWCPWMWCUFUGUHWMWRWCW
      KWLWRWCPWLWRWKWCWLWPUFUIUJUKULUMUNWPFLUOUPWPWFFALWBVOQWKVPWCWEWBVOBUQWBVO
      CUQURUSUTWIWNFWHWMWCWBVAGWHWMUAFVBVRWMAWBVAVOWBQVPWKVQWLVOWBBUQVOWBLVCVDV
      EVFVGVHVIWCFVSUOVIVJ $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A y $.
    $( The set ` A ` is an element of the smallest Tarski's class that contains
       ` A ` .  CLASSES1 th. 5. $)
    elttar $p |- ( A e. B -> A e. ( tarskiMap ` A ) ) $=
      ( vy wcel cv ctarski wa cab cint ctarskim cfv wi wal pm3.26 ax-gen
      elintabg mpbiri tarval2g eleqtrrd ) ABDZAACEZDZUAFDZGZCHIZAJKTAUEDUDUBLZC
      MUFCUBUCNOUDCABPQCABRS $.
      $( [30-Dec-2010] $)
  $}

  $( The intersection of all Tarski's classes is the empty set. $)
  inttar1 $p |- |^| Tarski = (/) $=
    ( c0 ctarski wcel cint wceq empistar int0el ax-mp ) ABCBDAEFBGH $.
    $( [20-Apr-2011] $)

  ${
    $d A t z $.
    $( The intersection of a collection of Tarski's classes is a Tarski's
       class. $)
    intartar $p |- ( ( A C_ Tarski /\ A =/= (/) ) -> |^| A e. Tarski ) $=
      ( vz vt ctarski wss c0 wne wa cint wcel cv cpw wral cen wbr wo wi dfss3
      r19.26 tarax1 ralimi ssint sylibr tarax2 grothpwex elint2 jca a1d sylbir
      ex com23 sylbi imp cvv visset elintg biimpd ax-mp syl5 r19.21aiv elpw
      r19.27av ssel tarax3 expcom syl9 adantr impcom com12 ralimia biimpri
      adantl wn wrex fordisxex intss1 sndw 3exp com4l mpi syl r19.23aiv imp31
      condisd orcom sylib mpd syl6 wb intex biimpi tarval1g mpbird ) ADEZAFGZHZ
      AIZDJZBKZLZWQEZWTWQJZHZBWQMZWSWQNOZWSWQJZPZBWQLZMZHZWPXDXIWPXCBWQWPWSCKZJ
      ZCAMZXCXFWNWOXMXCQZWNXKDJZCAMZWOXNQCADRXPXMWOXCXPXMWOXCQZXPXMHXOXLHZCAMZX
      QXOXLCASXSXCWOXSXAXBXSWTXKEZCAMXAXRXTCAWSXKTUACWTAUBUCXSWTXKJZCAMXBXRYACA
      WSXKUDUACWTABUEUFUCUGUHUIUJUKULUMWSUNJZXFXMQBUOZYBXFXMCWSAUNUPUQURUSUTWPX
      GBXHWSXHJZWPXGYDWSWQEZWPXGQZWSWQYCVAYEWSXKEZCAMZYFCWSAUBZYHWPYGWPHZCAMZXG
      YHWPYKYGWPCAVBUJYKWSXKNOZXLPZCAMZXGYJYMCAYJXKAJZYMWPYGYOYMQZWNYGYPQWOWNYO
      XOYGYMADXKVCYGWSXKLJZXOYMQWSXKYCVAXOYQYMWSXKVDVEUIVFVGVHVIVJYKYHWPCAMZHYN
      XGQZYGWPCASYHYSYRYHYEYSYIYNYEXGYNYEHZXFXEPXGYTXMXFXEXMXFYTXFXMCWSAYCUFVKV
      LYNYEXMVMZXEYNUUAYEXEYNUUAYEXEQZYNUUAHYLCAVNUUBYLXLCAVOYLUUBCAYOWQXKEZYLU
      UBQZXKAVPUUCXKUNJZUUDCUOYEUUCUUEYLXEYEUUCUUEYLXEQWSWQXKVQVRVSVTWAWBWAUJUK
      WCWDXFXEWEWFVEUIVGULWGWHULULVIUTUGWPWQUNJZWRXJWIWOUUFWNWOUUFAWJWKVLBUNWQW
      LWAWM $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A x $.  $d T x $.
    $( The intersection of an element of a transitive Tarski's class is an
       element of the class. $)
    intrtael $p |- ( ( T e. Tarski /\ Tr T /\ ( A e. T /\ A =/= (/) ) )
      -> |^| A e. T ) $=
      ( vx ctarski wcel cint wss csdm wbr wtr c0 wne wa w3a tarax3f 3simp1
      inttrp 3expb 3adant1 wi cv wex n0 intss1 cvv visset cdom ssdom2g trel
      domsdomtr ex tarax3d2 syl5com expcom syl6 exp3a com15 ax-mp mpcom
      19.23aiv sylbi impcom com3l 3imp syl3anc ) BDEZAFZBGZVGBHIZVGBEVFBJZABEZA
      KLZMZNVGBOVFVJVMPVJVMVHVFVJVKVLVHBAQRSVFVJVMVIVMVFVJVIVLVKVFVJVITTZVLCUAZ
      AEZCUBVKVNTZCAUCVPVQCVGVOGZVPVQVOAUDVOUEEZVRVPVQTZTCUFVSVRVGVOUGIZVTVGVOU
      EUHVJVPVKVFWAVIVJVPVKVFWAVITZTZVJVPVKMVOBEZWCBVOAUIVFWDWBWAVOBHIZVIVFWDMW
      AWEVIVGVOBUJUKVOBULUMUNUOUPUQUOURUSUTVAVBVCVDVE $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d X x $.  $d t x $.
    $( A Tarski's class that contains ` X ` is a Tarski's class. $)
    tmpts $p |- ( X e. A -> ( tarskiMap ` X ) e. Tarski ) $=
      ( vx vt cv ctarskim cfv ctarski wcel wceq fveq2 eleq1d cvv visset wa cab
      cint tarval2g wss c0 wne cin inab abid2 ineq2 inss2 sseq1 mpbiri syl
      ax-mp eqsstr3i wex wrex settrcon exancom df-rex bitr4i mpbir abn0
      intartar mp2an syl6eqel vtoclg ) CEZFGZHIZBFGZHICBAVDBJVEVGHVDBFKLVDMIZVF
      CNZVHVEVDDEZIZVJHIZOZDPZQZHDVDMRVNHSVNTUAZVOHIVNVKDPZVLDPZUBZHVKVLDUCVRHJ
      ZVSHSZDHUDVTVSVQHUBZJZWAVRHVQUEWCWAWBHSVQHUFVSWBHUGUHUIUJUKVPVMDULZWDVKDH
      UMZVHWEVIDVDMUNUJWDVLVKODULWEVKVLDUOVKDHUPUQURVMDUSURVNUTVAVBUJVC $.
      $( [17-Apr-2011] $)
  $}

  $c tar $. $( A function to study Tarski's classes. $)

  $( Extends class notation to include function ` tar ` . $)
  ctar $a class tar $.

  $( A function to study Tarski's classes.  See ~ valdom for its domain,
     ~ vtare for its value at ` (/) ` , ~ vtarsu for its value at a successor,
     ~ vtarl for ist value at a limit ordinal. $)
  df-tar $a |- tar = { <. <. a , b >. , c >. | c = ( rec
       ( { <. x , y >. | y = ( ( ( { u | E. v e. x u C_ v } u.
         { u | E. v e. x u = ~P v } ) u.
          ~P x ) i^i ( tarskiMap ` a ) ) } , { a } ) |` b ) } $.

  ${
    $d X a b c x $.  $d X a b c y $.  $d Y a b c $.  $d a b c u $.
    $d a b c v $.
    $( The ` tar ` function as a recursive function. $)
    valtar $p |- ( ( X e. A /\ Y e. B ) -> ( tar ` <. X , Y >. ) = ( rec
       ( { <. x , y >. | y = ( ( ( { u | E. v e. x u C_ v } u.
         { u | E. v e. x u = ~P v } ) u.
          ~P x ) i^i ( tarskiMap ` X ) ) } , { X } ) |` Y ) ) $=
      ( vc va vb wcel wa cop ctar cfv cv wss wrex cab cpw wceq cun ctarskim
      cin copab csn crdg cres copab2 co df-tar fveq1i a1i df-opr eqcomi cvv
      fveq2 ineq2d eqeq2d opabbidv rdgeq1 syl sneq rdgeq2 eqtrd reseq1 reseq2
      cxp wrel cdm reloprab reldmoprab resid2 mp2an resoprab eqtr3i oprabval2g
      elisset adantr adantl wfun con0 wfn rdgfnon fnfun resfunexg sylancom
      syl3anc 3eqtrd ) GELZHFLZMZGHNZOPZWNIQBQZDQZCQZRCAQZSDTWQWRUAUBCWSSDTUCWS
      UAUCZJQZUDPZUEZUBZABUFZXAUGZUHZKQZUIZUBZJKIUJZPZGHXKUKZWPWTGUDPZUEZUBZABU
      FZGUGZUHZHUIZWOXLUBWMWNOXKABCDJKIULUMUNXLXMUBWMXMXLGHXKUOUPUNGUQLZHUQLZXT
      UQLZXMXTUBWMJKIGHUQUQXIXTXKXSXHUIZUQXAGUBZXGXSUBXIYDUBYEXGXQXFUHZXSYEXEXQ
      UBXGYFUBYEXDXPABYEXCXOWPYEXBXNWTXAGUDURUSUTVAXFXEXQVBVCYEXFXRUBYFXSUBXAGV
      DXFXRXQVEVCVFXGXSXHVGVCXHHXSVHXKUQUQVIUIZXKXAUQLXHUQLMXJMJKIUJXKVJXKVKVJY
      GXKUBXJJKIVLXJJKIVMXKVNVOXJJKIUQUQVPVQVRWKYAWLGEVSVTWLYBWKHFVSWAWKWLXSWBZ
      YCWMXSWCWDZYHYIWMXRXQWEUNWCXSWFVCXSHFWGWHWIWJ $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d X x $.  $d X y $.
    $( The domain of the ` tar ` function is the ordinal ` Y ` . $)
    valdom $p |- ( ( X e. A /\ Y e. On )
      -> dom ( tar ` <. X , Y >. ) = Y ) $=
      ( vy vw vz vx cop ctar cfv cv wss wrex cab cpw wceq cun ctarskim cin
      copab csn crdg cres wcel con0 wa cdm valtar dmeq eqeq1d wfn wi rdgfnon
      ineq2 df-ss biimpi syl5bir onss syl5com adantl fndm ax-mp dmres syl5eq
      mpcom ) BCHIJZDKEKZFKZLFGKZMENVGVHOPFVIMENQVIOQBRJSPGDTZBUAZUBZCUCZPZBAUD
      ZCUEUDZUFZVFUGZCPZGDFEAUEBCUHVNVSVMUGZCPVQVNVRVTCVFVMUIUJVQCVLUGZSZCVTVLU
      EUKZVQWBCPZULVKVJUMVQWAUEPZWDWCVPWEWDULVOWECUELZWDVPWEWDCUESZCPZWFWEWBWGC
      WAUECUNUJWFWHCUEUOUPUQCURUSUTUEVLVAUSVBVLCVCVDUQVE $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d X x $.  $d X y $.
    $( Value of ` tar ` at ` (/) ` .  JFM CLASSES1 th. 10. $)
    vtare $p |- ( ( X e. A /\ Y e. On /\ Y =/= (/) )
     -> ( ( tar ` <. X , Y >. ) ` (/) ) = { X } ) $=
      ( vy vw vz vx wcel con0 c0 wne w3a cop ctar cfv csn wceq cv wss wrex cab
      cpw cun ctarskim cin copab crdg cres on0eln0 biimpar 3adant1 fvres syl
      cvv snex a1i rdg0g 3ad2ant1 eqtrd wb wa valtar fveq1d eqeq1d 3adant3
      mpbird ) BAHZCIHZCJKZLZJBCMNOZOZBPZQZJDRERZFRZSFGRZTEUAVOVPUBQFVQTEUAUCVQ
      UBUCBUDOUEQGDUFZVMUGZCUHZOZVMQZVJWAJVSOZVMVJJCHZWAWCQVHVIWDVGVHWDVICUIUJU
      KJCVSULUMVGVHWCVMQZVIVGVMUNHZWEWFVGBUOUPVMUNVRUQUMURUSVGVHVNWBUTVIVGVHVAZ
      VLWAVMWGJVKVTGDFEAIBCVBVCVDVEVF $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d A u x y $.  $d A v x y $.  $d X u x y $.  $d X v x y $.  $d Y u x y $.
    $d Y v x y $.
    $( Value of ` tar ` at ` suc A ` .  JFM CLASSES1 th. 11. $)
    vtarsu $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) -> ( ( tar `
    <. X , Y >. ) ` suc A ) = ( ( ( { u | E. v e. ( ( tar ` <. X , Y
    >. ) ` A ) u C_ v } u. { u | E. v e. ( ( tar ` <. X , Y >. ) ` A
    ) u = ~P v } ) u. ~P ( ( tar ` <. X , Y >. ) ` A ) ) i^i (
    tarskiMap ` X ) ) ) $=
      ( vy vx wcel con0 csuc w3a cop ctar cfv cv wss wrex cab cpw wceq cun
      ctarskim cin copab csn crdg cres valtar 3adant3 fveq1d fvres 3ad2ant3
      ordsuccl2 onelon syldan 3adant1 rdgsuc syl eqcomd fveq2d wa cvv rexeq
      abbidv uneq12d pweq ineq1d fvopabg fvex inex2 a1i sylancr eqtrd 3eqtrd )
      EDIZFJIZCKZFIZLZVREFMNOZOVRGPBPZAPZQZAHPZRZBSZWBWCTUAZAWERZBSZUBZWETZUBZE
      UCOZUDZUAHGUEZEUFZUGZFUHZOZVRWROZWDACWAOZRZBSZWHAXBRZBSZUBZXBTZUBZWNUDZVT
      VRWAWSVPVQWAWSUAVSHGABDJEFUIZUJUKVSVPWTXAUAVQVRFWRULUMVTXACWROZWPOZCWSOZW
      POZXJVTCJIZXAXMUAVQVSXPVPVQVSCFIZXPCFUNZFCUOUPUQWQCWPURUSVTXLXNWPVTXQXLXN
      UAVQVSXQVPXRUQXQXNXLCFWRULUTUSVAVTXOXBWPOZXJVTXNXBWPVTCWSWAVPVQWSWAUAVSVP
      VQVBWAWSXKUTUJUKVAXBVCIXJVCIZXSXJUAVTHGXBWOXJVCVCWEXBUAZWMXIWNYAWKXGWLXHY
      AWGXDWJXFYAWFXCBWDAWEXBVDVEYAWIXEBWHAWEXBVDVEVFWEXBVGVFVHVICWAVJXTVTWNXIE
      UCVJVKVLVMVNVOVO $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d X x $.  $d X y $.
    $( The value of ` tar ` at a limit ordinal.  JFM CLASSES1 th. 12. $)
    vtarl $p |- ( ( X e. B /\ Y e. On /\ ( A e. Y /\ Lim A ) )
      -> ( ( tar ` <. X , Y >. ) ` A ) = U. ( ( tar ` <. X , Y >. ) " A ) ) $=
      ( vy vu vv vx wcel con0 wlim wa w3a cop ctar cfv cv wss wrex cab cpw
      wceq cun ctarskim cin copab csn crdg cres cima cuni valtar fveq1d
      3adant3 pm3.26 3ad2ant3 fvres syl rdglim word imresord eloni 3ad2ant2
      sylanc eqcomd imaeq1d eqtr3d unieqd eqtrd 3eqtrd ) CBIZDJIZADIZAKZLZMZACD
      NOPZPZAEQFQZGQZRGHQZSFTVSVTUAUBGWASFTUCWAUAUCCUDPUEUBHEUFZCUGZUHZDUIZPZAW
      DPZVQAUJZUKZVKVLVRWFUBVOVKVLLAVQWEHEGFBJCDULZUMUNVPVMWFWGUBVOVKVMVLVMVNUO
      UPZADWDUQURVPWGWDAUJZUKZWIVOVKWGWMUBVLWCADWBUSUPVPWLWHVPWEAUJZWLWHDUTZVMW
      NWLUBVPADWDVAVLVKWOVODVBVCWKVDVPWEVQAVPVQWEVKVLVQWEUBVOWJUNVEVFVGVHVIVJ
      $.
      $( [20-Mar-2011] $)
  $}


  ${
    $d A b c $.  $d B b $.  $d X b c $.  $d X c u $.  $d X c v $.  $d X x $.
    $d X y $.  $d Y b c $.  $d Y c u $.  $d Y c v $.
    $( The sequence ` tar ` has its values in a Tarski's class. $)
    tartarmap $p |- ( ( X e. A /\ Y e. On /\ suc B e. Y ) ->
      ( ( tar ` <. X , Y >. ) ` B ) C_ ( tarskiMap ` X ) ) $=
      ( vb vc vu vv vy vw vz vx csuc con0 wcel w3a cop ctar cfv ctarskim wss
      onelon 3adant1 wi sucelon cv c0 wceq suceq eleq1d 3anbi3d fveq2 sseq1d
      imbi12d csn wne vtare ne0i syl3an3 elttar 3ad2ant1 wb snssg mpbid
      eqsstrd wrex cab cpw cun cin inss2 3simp1 3simp2 wtr trsuc word eloni
      ordtr syl 3ad2ant2 3simp3 sylanc 3jca 3ad2ant3 vtarsu mpbiri 3exp wlim
      wral cima cuni wa vtarl ordsuccl2 adantl pm3.26 jca 3adant2 syl3anc
      r19.27av ordon ordtr1 ax-mp sylbir ancoms ordsucelsuc 3simpl1 3simpl2
      a1i com12 ex com13 syl6 3imp1 expcom syl6bi pm2.43a pm5.31 com3r imp
      impcom ralimiaa r19.26 3simpa ralimi wfun cdm copab crdg cres valtar wfn
      rdgfnon fnfun funres funeq 3adant3 ordsuccl3 valdom sseqtr4d funimass4
      fvex elpw ralbii syl6bb uniss unipw syl6ss syl6bir com23 sylbi pm2.43d
      3imp tfinds mpcom ) BMZNOZCAOZDNOZUUPDOZPZBCDQRSZSZCTSZUAZUUSUUTUUQUURDUU
      PUBUCUUQBNOUVAUVEUDZBUEUURUUSEUFZMZDOZPZUVGUVBSZUVDUAZUDUURUUSUGMZDOZPZUG
      UVBSZUVDUAZUDUURUUSFUFZMZDOZPZUVRUVBSZUVDUAZUDZUURUUSUVSMZDOZPZUVSUVBSZUV
      DUAZUDUVFEFBUVGUGUHZUVJUVOUVLUVQUWJUVIUVNUURUUSUWJUVHUVMDUVGUGUIUJUKUWJUV
      KUVPUVDUVGUGUVBULUMUNUVGUVRUHZUVJUWAUVLUWCUWKUVIUVTUURUUSUWKUVHUVSDUVGUVR
      UIUJUKUWKUVKUWBUVDUVGUVRUVBULUMUNUVGUVSUHZUVJUWGUVLUWIUWLUVIUWFUURUUSUWLU
      VHUWEDUVGUVSUIUJUKUWLUVKUWHUVDUVGUVSUVBULUMUNUVGBUHZUVJUVAUVLUVEUWMUVIUUT
      UURUUSUWMUVHUUPDUVGBUIUJUKUWMUVKUVCUVDUVGBUVBULUMUNUVOUVPCUOZUVDUURUUSDUG
      UPUVPUWNUHUVNACDUQDUVMURUSUVOCUVDOZUWNUVDUAZUURUUSUWOUVNCAUTVAUURUUSUWOUW
      PVBUVNCUVDAVCVAVDVEUVRNOZUWDUWGUWIUWQUWDUWGPZUWIGUFZHUFZUAHUWBVFGVGUWSUWT
      VHUHHUWBVFGVGVIUWBVHVIZUVDVJZUVDUAZUXAUVDVKUWRUWAUWIUXCVBUWGUWQUWAUWDUWGU
      URUUSUVTUURUUSUWFVLUURUUSUWFVMDVNZUWFUVTUWGDUVSVOUUSUURUXDUWFUUSDVPZUXDDV
      QZDVRVSVTUURUUSUWFWAWBWCWDUWAUWHUXBUVDHGUVRACDWEUMVSWFWGUVGWHZUWDFUVGWIZU
      VJUVLUXGUXHUVJPZUVKUVBUVGWJZWKZUVDUURUUSUVGDOZUXGWLZUVKUXKUHUXIUVGACDWMUV
      JUXGUURUXHUURUUSUVIVLWDUVJUXGUUSUXHUURUUSUVIVMWDUXGUVJUXMUXHUXGUVJWLUXLUX
      GUVJUXLUXGUUSUVIUXLUURUVGDWNUCWOUXGUVJWPWQWRWSUXGUXHUVJUXKUVDUAZUXHUVJUXN
      UDZUDUXGUXHUVJUXNUXHUVJUXOUXHUVJWLUWDUVJWLZFUVGWIZUXOUWDUVJFUVGWTUXQUWCUW
      AWLZFUVGWIZUXOUXPUXRFUVGUXPUVRUVGOZUXRUWDUVJUXTUXRUDUVJUXTUWDUXRUVJUXTUWA
      UWDUXRUDUXTUVJUWAUVJUVGVPZUXTUVJUWAUDZUDUUSUVIUYAUURUVIUUSUYAUVIUUSWLZUVH
      NOZUYANVPUYCUYDUDXAUVHDNXBXCUYDUVGNOUYAUVGUEUVGVQXDVSXEUCUYAUXTUVSUVHOZUY
      BUVRUVGXFUVJUYEUWAUVJUYEWLUURUUSUVTUURUUSUVIUYEXGUURUUSUVIUYEXHUURUUSUVIU
      YEUVTUURUUSUXEUVIUYEUVTUDUDUUSUXEUDUURUXFXIUYEUVIUXEUVTUYEUVIUXEUVTUDUXEU
      YEUVIWLUVTUVSUVHDXBXJXKXLXMXNWCXOXPVSXQUWDUWAUXRUWAUWDUWAUXRUDUWAUWCUWAXR
      XKXQXMXSXTYAYBUXSUWCFUVGWIZUWAFUVGWIZWLUXOUWCUWAFUVGYCUYGUYFUXOUYGUURUUSW
      LZFUVGWIZUYFUXOUDUWAUYHFUVGUURUUSUVTYDYEUYIUVJUYFUXNUYIUVJUVBYFZUVGUVBYGZ
      UAZWLZUYFUXNUDUYIUVJUYMUYIUVJWLUYJUYLUVJUYJUYIUURUUSUYJUVIUYHUVBIUFJUFZKU
      FZUAKLUFZVFJVGUYNUYOVHUHKUYPVFJVGVIUYPVHVIUVDVJUHLIYHZUWNYIZDYJZUHZUYJLIK
      JANCDYKUYTUYJUYSYFZUYRYFZVUAUYRNYLVUBUWNUYQYMNUYRYNXCDUYRYOXCUVBUYSYPWFVS
      YQWOUVJUYLUYIUVJUVGDUYKUUSUVIUVGDUAUURUVGDYRUCUURUUSUYKDUHUVIACDYSYQYTWOW
      QXKUYMUYFUXJUVDVHZUAZUXNUYMVUDUWBVUCOZFUVGWIUYFFUVGVUCUVBUUAVUEUWCFUVGUWB
      UVDUVRUVBUUBUUCUUDUUEVUDUXKVUCWKUVDUXJVUCUUFUVDUUGUUHUUIXMUUJVSYAUUKVSVSX
      KUULXIUUMVEWGUUNXDUUO $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d A t $.  $d X t $.
    $( If ` A ` belongs to the smallest Tarski's class that contains ` X ` so
       does ` ~P A ` .  CLASSES1 th. 7. $)
    pwtsm $p |- ( ( X e. B /\ A e. ( tarskiMap ` X ) )
      -> ~P A e. ( tarskiMap ` X ) ) $=
      ( vt wcel ctarskim cfv wa cpw cv wi ctarski wral btmp biimpd ex pm2.43d
      imp tarax2 imim2d ralimia syl cvv wb pwexg sylan2 mpbird ) CBEZACFGZEZHZA
      IZUIEZCDJZEZULUNEZKZDLMZUKUOAUNEZKZDLMZURUHUJVAUHUJVAUHUJUJVAKUKUJVADCABU
      INOPQRUTUQDLUNLEZUSUPUOVBUSUPAUNSPTUAUBUHULUCEUMURUDUJDCULBUCNAUIUEUFUG
      $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A t $.  $d X t $.
    $( If ` A ` belongs to the smallest Tarski's class that contains ` X ` so
       does the subsets of ` A ` .  CLASSES1. th. 6. $)
    subtsm $p |- ( ( X e. B /\ A e. ( tarskiMap ` X ) )
      -> ~P A C_ ( tarskiMap ` X ) ) $=
      ( vt wcel ctarskim cfv wa cpw wss cv wi ctarski wral btmp tarax1 ex
      imim2d ralimia syl6bi pm2.43d imp cvv wb bpmp pwexg sylan2 mpbird ) CBEZA
      CFGZEZHZAIZUJJZCDKZEZUMUOJZLZDMNZUIUKUSUIUKUSUIUKUKUSLULUKUPAUOEZLZDMNUSD
      CABUJOVAURDMUOMEZUTUQUPVBUTUQAUOPQRSTQUAUBUIUMUCEUNUSUDUKDCUMBUCUEAUJUFUG
      UH $.
      $( [17-Apr-2011] $)
  $}

  $( If ` A ` is a subset of the smallest Tarski's class that contains ` X `
     then it is equipotent to this class or it belongs to it.  CLASSES1 th.
     8. $)
  subtareqbe $p |- ( ( X e. B /\ A e. ~P ( tarskiMap ` X ) ) ->
      ( A ~~ ( tarskiMap ` X ) \/ A e. ( tarskiMap ` X ) ) ) $=
    ( ctarskim cfv ctarski wcel cpw cen wbr wo tarax3 tmpts sylan ) CDEZFGAOHGA
    OIJAOGKCBGAOLBCMN $.
    $( [17-Apr-2011] $)

  $c tr $.

  $( $)

  $( Extend class notation to include the function whose value is the
     transitive closure of its operand. $)
  ctr $a class tr $.

  ${
    $d x y a z w $.
    $( The transitive closure of a set. $)
    df-trcls $a |- tr = { <. x , y >. | y = U_ a e. om ( ( rec ( { <. z , w >.
      | w = ( z u. U. z ) } , x ) |` om ) ` a ) } $.
  $}

  ${
    $d A a x y $.  $d a w x y z $.
    $( The transitive closure of a set A. $)
    trclval $p |- ( A e. B
    -> ( tr ` A ) = U_ a e. om
     ( ( rec ( { <. z , w >. | w = ( z u. U. z ) } , A ) |` om ) ` a ) ) $=
      ( vx vy cvv wcel com cv cuni cun wceq copab crdg cres cfv ciun ctr
      rdgeq2 reseq1 syl fveq1d adantr iuneq2dv wa df-trcls wrel relopab resid
      ax-mp resopab 3eqtr2i fvopab4g elisset wral omex fvex a1i rgen iunexg
      mp2an sylancl ) CHIEJEKZBKAKZVFLMNABOZCPZJQZRZSZHIZCTRVKNCDIFGCEJVEVGFKZP
      ZJQZRZSZVKHHTVMCNZEJVPVJVRVPVJNVEJIZVRVEVOVIVRVNVHNVOVINVMCVGUAVNVHJUBUCU
      DUEUFTGKVQNZFGOZWAHQZVMHIVTUGFGOFGABEUHWAUIWBWANVTFGUJWAUKULVTFGHUMUNUOCD
      UPJHIVJHIZEJUQVLURWCEJWCVSVEVIUSUTVAEJVJHHVBVCVD $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A u v $.  $d A v z $.  $d B v $.  $d C u v $.  $d C v z $.  $d X u v $.
    $d X v z $.  $d Y u v $.  $d Y v z $.
    $( C belongs to the value of ` tar ` at a successor of ` A ` iff it is a
       part of ` tar ` at ` A ` , the powerset of an element or a part of an
       element of ` tar ` at ` A ` .  CLASSES1 th. 13 $)
    vtarsuelt $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) -> ( C e. ( ( tar
    ` <. X , Y >. ) ` suc A ) <-> ( ( C C_ ( ( tar ` <. X , Y >. ) ` A ) /\
    C e. ( tarskiMap ` X ) ) \/ E. z e. ( ( tar ` <. X , Y >. ) ` A )
    ( C C_ z \/ C = ~P z ) ) ) ) $=
      ( vu vv wcel con0 csuc w3a cop ctar cfv cv wss wrex cab cpw wceq cun
      ctarskim cin wa wo vtarsu eleq2d wb elin a1i elun anbi1d andir orbi1d wi
      elpwg pm5.32rd sseq1 rexbidv elabg eqeq1 orbi12d r19.43 bicomi tartarmap
      sseld imp visset elpw2 ssel com12 sylbir subtsm syl5com ex com23 a1d
      3ad2ant1 eleq1 imbi2d pwtsm syl5cbir adantr jaoi com13 mpcom pm4.71
      sylib bicomd rexbidva r19.41v sseq2 pweq eqeq2d cbvrexv 3bitr3g 3bitrd
      orcom syl5bb bitrd ) ECIZFJIZBKZFIZLZDXDEFMNOZOZIDGPZHPZQZHBXGOZRZGSZXIXJ
      TZUAZHXLRZGSZUBZXLTZUBZEUCOZUDZIZDXLQZDYBIZUEZDAPZQZDYHTZUAZUFZAXLRZUFZXF
      XHYCDHGBCEFUGUHXFYDDYAIZYFUEZDXSIZDXTIZUFZYFUEZYNYDYPUIXFDYAYBUJUKXFYOYSY
      FYOYSUIXFDXSXTULUKUMXFYTYQYFUEZYRYFUEZUFZDXNIZDXRIZUFZYFUEZUUBUFZYNYTUUCU
      IXFYQYRYFUNUKXFUUAUUGUUBXFYQUUFYFYQUUFUIXFDXNXRULUKUMUOXFUUBUUGUFYNUUHXFU
      UBYGUUGYMXFYFYRYEYFYRYEUIUPXFDXLYBUQUKURXFUUGDXJQZHXLRZDXOUAZHXLRZUFZYFUE
      UUIUUKUFZHXLRZYFUEZYMXFYFUUFUUMYFUUFUUMUIUPXFYFUUDUUJUUEUULXMUUJGDYBXIDUA
      ZXKUUIHXLXIDXJUSUTVAXQUULGDYBUUQXPUUKHXLXIDXOVBUTVAVCUKURXFUUMUUOYFUUMUUO
      UIXFUUOUUMUUIUUKHXLVDVEUKUMXFUUNYFUEZHXLRUUOUUPYMXFUURUUNHXLXFXJXLIZUEZUU
      NUURUUTUUNYFUPZUUNUURUIXJYBIZUUTUVAXFUUSUVBXFXLYBXJCBEFVFVGVHUUNUUTUVBYFU
      UIUUTUVBYFUPZUPUUKUUTUUIUVCXFUUSUUIUVCUPZXBXCUUSUVDUPXEXBUVDUUSXBUVBUUIYF
      XBUVBUUIYFUPUUIXOYBQZYFXBUVBUEUUIDXOIZUVEYFUPDXJHVIVJUVEUVFYFXOYBDVKVLVMX
      JCEVNVOVPVQVRVSVHVLUUTUUKUVCXFUUKUVCUPZUUSXBXCUVGXEUUKUVCUVBXOYBIZUPXBUUK
      YFUVHUVBDXOYBVTWAXBUVBUVHXJCEWBVPWCVSWDVLWEWFWGUUNYFWHWIWJWKUUNYFHXLWLUUN
      YLHAXLXJYHUAZUUIYIUUKYKXJYHDWMUVIXOYJDXJYHWNWOVCWPWQWRVCUUGUUBWSWTWRWRXA
      $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A z $.  $d C z $.  $d X z $.  $d Y z $.  $d Z z $.
    $( If ` C ` is a part of an element of our tar function at ` A ` then ` C `
       is an element or tar at ` suc A ` .  CLASSES1 th. 14 $)
    partarelt1 $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( ( C C_ Z /\ Z e. ( ( tar ` <. X , Y >. ) ` A ) ) ->
          C e. ( ( tar ` <. X , Y >. ) ` suc A ) ) ) $=
      ( vz wss cop ctar cfv wcel wa con0 csuc w3a cpw wceq wo wi orc cv wrex
      sseq2 pweq eqeq2d orbi12d rcla4ev ctarskim vtarsuelt olc syl5cbir syl ex
      mpan9 com12 ) CFHZFADEIJKZKZLZMDBLENLAOZELPZCVAURKLZUQUQCFQZRZSZUTVBVCTZU
      QVEUAUTVFVGUTVFMCGUBZHZCVHQZRZSZGUSUCZVGVLVFGFUSVHFRZVIUQVKVEVHFCUDVNVJVD
      CVHFUEUFUGUHVBVCCUSHCDUIKLMZVMSVMGABCDEUJVMVOUKULUMUNUOUP $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A u v $.  $d X u v $.  $d Y u v $.  $d Z u v $.
    $( If ` Z ` is an element of our tar function at ` A ` then ` ~P Z ` is an
       element or tar at ` suc A ` .  CLASSES1 th. 15 $)
    partarelt2 $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( Z e. ( ( tar ` <. X , Y >. ) ` A ) ->
          ~P Z e. ( ( tar ` <. X , Y >. ) ` suc A ) ) ) $=
      ( vu vv wcel con0 csuc w3a cop ctar cfv cpw wa cv wss wrex cab wceq cun
      ctarskim cin elin pweq eqeq2d rcla4ev pm3.27 eqidd sylanc wb cvv pwexg
      eqeq1 rexbidv elabg syl adantl mpbird elun2 elun1 3syl pwtsm 3simpl1
      tartarmap sseld imp sylanbrc vtarsu adantr eleqtrrd ex ) CBHZDIHZAJZDHZKZ
      EACDLMNZNZHZEOZVPVSNZHVRWAPZWBFQZGQZRGVTSFTZWEWFOZUAZGVTSZFTZUBZVTOZUBZCU
      CNZUDZWCWBWPHWBWNHZWBWOHZWDWBWNWOUEWDWBWKHZWBWLHWQWDWSWBWHUAZGVTSZWAWBWBU
      AZXAWDWTXBGEVTWFEUAWHWBWBWFEUFUGUHVRWAUIWDWBUJUKWAWSXAULZVRWAWBUMHXCEVTUN
      WJXAFWBUMWEWBUAWIWTGVTWEWBWHUOUPUQURUSUTWBWKWGVAWBWLWMVBVCVNEWOHZWRWDEBCV
      DVNVOVQWAVEVRWAXDVRVTWOEBACDVFVGVHUKVIVRWCWPUAWAGFABCDVJVKVLVM $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d X x $.  $d Y x $.
    $( All the element of ` tar ` at ` A ` are elements of ` tar ` at
       ` suc A ` .  CLASSES1 th. 18 $)
    tareltsuc $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( ( tar ` <. X , Y >. ) ` A ) C_
         ( ( tar ` <. X , Y >. ) ` suc A ) ) $=
      ( vx wcel con0 csuc w3a cop ctar cfv cv wss ssid partarelt1 mpani ssrdv
      ) CBFDGFAHZDFIZEACDJKLZLZSUALZTEMZUDNUDUBFUDUCFUDOABUDCDUDPQR $.
      $( [13-Apr-2011] $)
  $}

  $( An element of the intersection of a Tarski's class with the class of the
     ordinal numbers is a part of the intersection. $)
  eltintpar $p |- ( T e. Tarski -> ( A e. ( On i^i T )
      -> A C_ ( On i^i T ) ) ) $=
    ( ctarski wcel con0 cin wss wa wi elin onss ad2antrr word wtr eloni ordtr
    tartrel 3exp com3l 3syl imp31 jca ex sylbi impcom ssin sylib ) BCDZAEBFZDZA
    UIGZUHUJHAEGZABGZHZUKUJUHUNUJAEDZABDZHZUHUNIAEBJUQUHUNUQUHHULUMUOULUPUHAKLU
    OUPUHUMUOAMANZUPUHUMIIAOAPUHURUPUMUHURUPUMABQRSTUAUBUCUDUEAEBUFUGUC $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The intersection of a Tarski's class with the class of the ordinal
       numbers is an ordinal number. $)
    inttaror $p |- ( T e. Tarski -> ( On i^i T ) e. On ) $=
      ( vx con0 cin cvv wcel cv wss wa wral ctarski celsor inex1g incom
      syl5eqelr inss1 sseli adantl wi elin word wtr eloni ordtr onss w3a ssin
      biimpi ex tartrel syl5com 3exp com14 syl com12 3syl pm2.43i imp sylbi
      impcom jca r19.21aiv sylanc ) CADZEFBGZCFZVEVDHZIZBVDJVDCFAKFZBVDELVIACDE
      VDACKMACNOVIVHBVDVIVEVDFZVHVIVJIVFVGVJVFVIVDCVECAPQRVJVIVGVJVFVEAFZIVIVGS
      ZVECATVFVKVLVFVKVLSZVFVEUAVEUBZVFVMSVEUCVEUDVFVNVMVFVECHZVNVMSVEUEVIVNVKV
      OVGVIVNVKVOVGSVOVEAHZVGVIVNVKUFVOVPVGVOVPIVGVECAUGUHUIVEAUJUKULUMUNUOUPUQ
      URUSUTVAUIVBVC $.
      $( [20-Apr-2011] $)
  $}

  $( The intersection of a Tarski's class and the ordinal numbers is equipotent
     to the Tarski's class.  JFM CLASSES2. . $)
  inttarcar $p |- ( T e. Tarski -> ( On i^i T ) ~~ T ) $=
    ( ctarski wcel con0 cin wss cen wbr inss2 wo wa tarax3a wi ax-1 inttaror
    elin elirr pm2.21i sylbir ex syl adantr com12 jaoi mpcom mpan2 ) ABCZDAEZAF
    ZUHAGHZDAIUJUHACZJUGUIKZUJUHALUJULUJMUKUJULNULUKUJUGUKUJMZUIUGUHDCZUMAOUNUK
    UJUNUKKUHUHCZUJUHDAPUOUJUHQRSTUAUBUCUDUEUF $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The cardinal of the intersection of a Tarski's class with the class of
       the ordinal numbers. $)
    carinttar $p |- ( T e. Tarski
      -> ( card ` ( On i^i T ) ) = ( On i^i T ) ) $=
      ( vx con0 cin ccrd cfv wceq wcel cv csdm wbr wral ctarski iscard
      inttaror cdom cen wn wa brsdom wss eltintpar cvv wi inex1g incom
      syl5eqel ssdom2g syl syld imp inttarcar elin tarax3d2 sdomnen entr
      expcom con3d a1i com3r mpcom adantl sylbi com13 sylanbrc ex r19.21aiv )
      CADZEFVHGVHCHBIZVHJKZBVHLAMHZBVHNAOVKVJBVHVKVIVHHZVJVJVIVHPKZVIVHQKZRZVKV
      LSVIVHTVKVLVMVKVLVIVHUAZVMVIAUBVKVHUCHVPVMUDVKACDUCVHACMUECAUFUGVIVHUCUHU
      IUJUKVKVLVOVHAQKZVKVLVOUDAULVLVKVQVOVLVICHZVIAHZSVKVQVOUDZUDZVICAUMVSWAVR
      VKVSVTVIAJKZVKVSSZVTVIAUNWBVIAQKZRZWCVTUDVIAUOWCVQWEVOVQWEVOUDUDWCVQVNWDV
      NVQWDVIVHAUPUQURUSUTUIVAUQVBVCVDVAUKVEVFVGVE $.
      $( [20-Apr-2011] $)
  $}

  $( The cardinal of a Tarski's class equals the intersection of the Tarski's
     class with the class of the ordinal numbers.  CLASSES2 th. 10. $)
  carinttar2 $p |- ( T e. Tarski
      -> ( card ` T ) = ( On i^i T ) ) $=
    ( ctarski wcel ccrd cfv con0 cin wceq cen wbr inttarcar ensymg mpd wb
    inttaror carden mpdan mpbird carinttar eqtrd ) ABCZADEZFAGZDEZUCUAUBUDHZAUC
    IJZUAUCAIJUFAKUCABLMUAUCFCUEUFNAOAUCBFPQRAST $.
    $( [20-Apr-2011] $)
  ${
    $d T x $.
    $( The cardinal of an element of a Tarski's class belongs to the Tarski's
       class. th. 12 CLASSES2 $)
    cardtar $p |- ( ( T e. Tarski /\ A e. T ) -> ( card ` A ) e. T ) $=
      ( ccrd cfv con0 cin wceq wcel ctarski wa eleq2 elin pm3.27bi syl6bi
      carinttar2 adantr tarax3d3 sylc ) BCDZEBFZGZACDZSHZUBBHZBIHZABHZJUAUCUBTH
      ZUDSTUBKUGUBEHUDUBEBLMNUEUAUFBOPABQR $.
      $( [20-Apr-2011] $)

    $( The cardinal of a Tarski's class is a limit ordinal.  CLASSES2 th.
       21. $)
    cartarlim $p |- ( ( T e. Tarski /\ T =/= (/) ) -> Lim ( card ` T ) ) $=
      ( vx ctarski wcel c0 wne wa ccrd cfv wlim con0 cin word cv csuc wral w3a
      inttaror adantr cvv wb inex1g incom syl5eqel elong syl mpbid emptar
      0elon jctil elin sylibr wi suceloni ad2antrr visset elon tarsuc2 3exp
      com3l sylbi imp31 sylanbrc ex com12 r19.21aiv 3jca dflim4 wceq
      carinttar2 limeq mpbird ) ACDZAEFZGZAHIZJZKALZJZVOVRMZEVRDZBNZOZVRDZBVRPZ
      QVSVOVTWAWEVOVRKDZVTVMWFVNARSVOVRTDZWFVTUAVMWGVNVMAKLTVRAKCUBKAUCUDSVRTUE
      UFUGVOEKDZEADZGWAVOWIWHAUHUIUJEKAUKULVOWDBVRWBVRDZVOWDWJWBKDZWBADZGZVOWDU
      MWBKAUKWMVOWDWDWCKDZWCADZWMVOGWCKAUKWKWNWLVOWBUNUOWKWLVOWOWKWBMZWLVOWOUMU
      MWBBUPUQVOWPWLWOVMWPWLWOUMUMVNVMWPWLWOWBAURUSSUTVAVBVCVDVAVEVFVGBVRVHULVO
      VPVRVIZVQVSUAVMWQVNAVJSVPVRVKUFVL $.
      $( [20-Apr-2011] $)
  $}

