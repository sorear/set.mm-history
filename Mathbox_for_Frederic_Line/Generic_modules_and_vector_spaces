$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Generic modules and vector spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Vec $.

  $( Extend class notation with the class of all generic vector spaces and
     modules. $)
  cvec $a class Vec $.

  ${
    $d g h s a b u x v y $.
    $( Definition of a vector space ( ` <. g , h >. ` is a field ), or of a
       module ( ` <. g , h >. ` is a ring ). $)
    df-vec $a |- Vec = { z | E. g E. h E. a E. b
     ( z = <. <. g , h >. , <. a , b >. >. /\
        ( a e. AbelOp
         /\ b : ( ran g X. ran a ) --> ran a
         /\ A. u e. ran a ( ( ( Id ` h ) b u ) = u /\ A. x e. ran g
           ( A. v e. ran a ( x b ( u a v ) ) = ( ( x b u ) a ( x b v ) )
              /\ A. y e. ran g ( ( ( x g y ) b u ) = ( ( x b u ) a ( y b u ) )
              /\ ( ( x h y ) b u ) = ( x b ( y b u ) ) ) ) ) ) ) } $.
  $}


  ${
    $d A a b g h u v x y $.  $d B a b g h u v x y $.  $d G a b g h u x y $.
    $d H a b g h u x y $.  $d a b g h u v x y z $.
    vecval1b.1 $e |- X = ran G $.
    vecval1b.4 $e |- W = ran A $.
    $( The predicate "is a vector space" or "is a module".  (Contributed by FL,
       12-Jul-2010.) $)
    vecval1b $p |- ( ( ( G e. M /\ H e. N /\ A e. O ) /\ B e. P )
      -> ( <. G , H >. Vec <. A , B >. <->
      ( A e. AbelOp /\ B : ( X X. W ) --> W
       /\ A. u e. W ( ( ( Id ` H ) B u ) = u
          /\ A. x e. X (
             A. v e. W
                 ( x B ( u A v ) ) = ( ( x B u ) A ( x B v ) )
             /\ A. y e. X ( ( ( x G y ) B u ) = ( ( x B u ) A ( y B u ) )
             /\ ( ( x H y ) B u ) = ( x B ( y B u ) ) ) ) ) ) ) ) $=
      ( wa co wceq wral vz vg vh va vb wcel w3a cop cvec wbr crn cxp wf cgi cfv
      cabl cv df-br wex cab df-vec eleq2i wb rneq xpeq1 feq2 3syl opreq opreq1d
      eqeq1d anbi1d raleqbidv anbi2d ralbidv 3anbi23d fveq2 anbi12d eleq1 xpeq2
      3anbi3d syl feq23d opreq2d eqeq12d eqeq2d 3anbi123d opreq12d id opreq123d
      feq1 elo syl5bb biid eqcomi xpeq12i feq23i imbi1i ralbii2 anbi12i imbi12i
      eqidd anbi2i 3anbi123i syl6bb ) HJUFIKUFELUFUGFGUFQZHIUHZEFUHZUIUJZEUPUFZ
      HUKZEUKZULZXKFUMZIUNUOZDUQZFRZXOSZAUQZXOCUQZERZFRZXRXOFRZXRXSFRZERZSZCXKT
      ZXRBUQZHRZXOFRZYBYGXOFRZERZSZXRYGIRZXOFRZXRYJFRZSZQZBXJTZQZAXJTZQZDXKTZUG
      ZXINMULZMFUMZXQYECMTZYQBNTZQZANTZQZDMTZUGXHXFXGUHZUIUFZXEUUCXFXGUIURUUMUU
      LUAUQUBUQZUCUQZUHUDUQZUEUQZUHUHSUUPUPUFZUUNUKZUUPUKZULZUUTUUQUMZUUOUNUOZX
      OUUQRZXOSZXRXOXSUUPRZUUQRZXRXOUUQRZXRXSUUQRZUUPRZSZCUUTTZXRYGUUNRZXOUUQRZ
      UVHYGXOUUQRZUUPRZSZXRYGUUORZXOUUQRZXRUVOUUQRZSZQZBUUSTZQZAUUSTZQZDUUTTZUG
      ZQUEUSUDUSUCUSUBUSUAUTZUFXEUUCUIUWIUULABUACDUBUCUDUEVAVBUWHUURXJUUTULZUUT
      UUQUMZUVEUVLYHXOUUQRZUVPSZUWAQZBXJTZQZAXJTZQZDUUTTZUGUURUWKXNXOUUQRZXOSZU
      VLUWMYMXOUUQRZUVTSZQZBXJTZQZAXJTZQZDUUTTZUGXIXLXKUUQUMZUXAXRXTUUQRZUVHUVI
      ERZSZCXKTZUWLUVHUVOERZSZUXCQZBXJTZQZAXJTZQZDXKTZUGUUCUAUBUCUEUDHIEFJKLGUU
      NHSZUVBUWKUWGUWSUURUYCUUSXJSUVAUWJSUVBUWKVCUUNHVDZUUSXJUUTVEUVAUWJUUTUUQV
      FVGUYCUWFUWRDUUTUYCUWEUWQUVEUYCUWDUWPAUUSXJUYDUYCUWCUWOUVLUYCUWBUWNBUUSXJ
      UYDUYCUVQUWMUWAUYCUVNUWLUVPUYCUVMYHXOUUQXRYGUUNHVHVIVJVKVLVMVLVMVNVOUUOIS
      ZUWSUXIUURUWKUYEUWRUXHDUUTUYEUVEUXAUWQUXGUYEUVDUWTXOUYEUVCXNXOUUQUUOIUNVP
      VIVJUYEUWPUXFAXJUYEUWOUXEUVLUYEUWNUXDBXJUYEUWAUXCUWMUYEUVSUXBUVTUYEUVRYMX
      OUUQXRYGUUOIVHVIVJVMVNVMVNVQVNVTUUPESZUURXIUWKUXJUXIUYBUUPEUPVRUYFUWJUUTX
      LXKUUQUYFUUTXKSUWJXLSUUPEVDZUUTXKXJVSWAUYGWBUYFUXHUYADUUTXKUYGUYFUXGUXTUX
      AUYFUXFUXSAXJUYFUVLUXNUXEUXRUYFUVKUXMCUUTXKUYGUYFUVGUXKUVJUXLUYFUVFXTXRUU
      QXOXSUUPEVHWCUVHUVIUUPEVHWDVLUYFUXDUXQBXJUYFUWMUXPUXCUYFUVPUXOUWLUVHUVOUU
      PEVHWEVKVNVQVNVMVLWFUUQFSZUXJXMUYBUUBXIXLXKUUQFWJUYHUYAUUADXKUYHUXAXQUXTY
      TUYHUWTXPXOXNXOUUQFVHVJUYHUXSYSAXJUYHUXNYFUXRYRUYHUXMYECXKUYHUXKYAUXLYDXR
      XTUUQFVHUYHUVHYBUVIYCEXRXOUUQFVHZXRXSUUQFVHWGWDVNUYHUXQYQBXJUYHUXPYLUXCYP
      UYHUWLYIUXOYKYHXOUUQFVHUYHUVHYBUVOYJEUYIYGXOUUQFVHZWGWDUYHUXBYNUVTYOYMXOU
      UQFVHUYHXRXRUVOYJUUQFUYHWHUYHXRXAUYJWIWDVQVNVQVNVQVNVOWKWLWLXIXIXMUUEUUBU
      UKXIWMXLXKUUDMFXJNXKMNXJOWNZMXKPWNZWOUYLWPUUAUUJDXKMXOXKUFXOMUFUUAUUJXKMX
      OUYLVBYTUUIXQYSUUHAXJNXRXJUFXRNUFYSUUHXJNXRUYKVBYFUUFYRUUGYEYECXKMXSXKUFX
      SMUFYEXKMXSUYLVBWQWRYQYQBXJNYGXJUFYGNUFYQXJNYGUYKVBWQWRWSWTWRXBWTWRXCXD
      $.
      $( [12-Jul-2010] $)
  $}

  ${
    $d A a b g h $.  $d B a b g h $.  $d G a b g h $.  $d H a b g h $.
    $d R a b g h u v x y z $.  $d W a b g h $.  $d X a b g h $.
    vecval3b.1 $e |- X = ran G $.
    vecval3b.2 $e |- G = ( 1st ` ( 1st ` R ) ) $.
    vecval3b.3 $e |- H = ( 2nd ` ( 1st ` R ) ) $.
    vecval3b.4 $e |- A = ( 1st ` ( 2nd ` R ) ) $.
    vecval3b.5 $e |- B = ( 2nd ` ( 2nd ` R ) ) $.
    vecval3b.6 $e |- W = ran A $.
    $( The "axioms" of a vector space or module.  (Contributed by FL,
       12-Jul-2010.) $)
    vecval3b $p |- ( R e. Vec ->
      ( A e. AbelOp /\ B : ( X X. W ) --> W
       /\ A. u e. W ( ( ( Id ` H ) B u ) = u
          /\ A. x e. X (
             A. v e. W
                 ( x B ( u A v ) ) = ( ( x B u ) A ( x B v ) )
             /\ A. y e. X ( ( ( x G y ) B u ) = ( ( x B u ) A ( y B u ) )
             /\ ( ( x H y ) B u ) = ( x B ( y B u ) ) ) ) ) ) ) $=
      ( wceq co wral vz vg vh va vb cvec wcel cv cop cabl crn cxp wf cgi cfv wa
      w3a wex cab df-vec eleq2i c1st c2nd rneq xpeq1 feq2d eleq2d opreq opreq1d
      syl eqeq1d anbi1d imbi12d ralbidv2 anbi2d raleqbidv ralbidv fveq2 anbi12d
      3anbi23d 3anbi3d eleq1 xpeq2 feq23d opreq2d eqeq12d eqeq2d 3anbi123d feq1
      opreq12d id eqidd opreq123d eqcomi eleq1i a1i rneqi eqtr4i xpeq12i feq123
      mp3an fveq2i equid opreq123i eqeq1i eqeq12i imbi12i ralbii2 anbi12i bitrd
      wb opreqi eloi sylbi ) GUFUGGUAUHUBUHZUCUHZUIUDUHZUEUHZUIUIRXQUJUGZXOUKZX
      QUKZULZYAXRUMZXPUNUOZDUHZXRSZYERZAUHZYECUHZXQSZXRSZYHYEXRSZYHYIXRSZXQSZRZ
      CYATZYHBUHZXOSZYEXRSZYLYQYEXRSZXQSZRZYHYQXPSZYEXRSZYHYTXRSZRZUPZBXTTZUPZA
      XTTZUPZDYATZUQZUPUEURUDURUCURUBURUAUSZUGEUJUGZKJULZJFUMZIUNUOZYEFSZYERZYH
      YEYIESZFSZYHYEFSZYHYIFSZESZRZCJTZYHYQHSZYEFSZUVCYQYEFSZESZRZYHYQISZYEFSZY
      HUVJFSZRZUPZBKTZUPZAKTZUPZDJTZUQZUFUUNGABUACDUBUCUDUEUTVAUUMXSGVBUOZVBUOZ
      UKZYAULZYAXRUMZYGYPYHYQUWESZYEXRSZUUARZUUFUPZBUWFTZUPZAUWFTZUPZDYATZUQXSU
      WHUWDVCUOZUNUOZYEXRSZYERZYPUWKYHYQUWRSZYEXRSZUUERZUPZBUWFTZUPZAUWFTZUPZDY
      ATZUQGVCUOZVBUOZUJUGZUWFUXLUKZULZUXNXRUMZUXAYHYEYIUXLSZXRSZYLYMUXLSZRZCUX
      NTZUWJYLYTUXLSZRZUXDUPZBUWFTZUPZAUWFTZUPZDUXNTZUQZUWCUAUBUCUEUDGXOUWERZYC
      UWHUULUWQXSUYKYBUWGYAXRUYKXTUWFRYBUWGRXOUWEVDZXTUWFYAVEVJVFUYKUUKUWPDYAUY
      KUUJUWOYGUYKUUIUWNAXTUWFUYLUYKUUHUWMYPUYKUUGUWLBXTUWFUYKYQXTUGYQUWFUGZUUG
      UWLUYKXTUWFYQUYLVGUYKUUBUWKUUFUYKYSUWJUUAUYKYRUWIYEXRYHYQXOUWEVHVIVKVLVMV
      NVOVPVOVQVTXPUWRRZUWQUXJXSUWHUYNUWPUXIDYAUYNYGUXAUWOUXHUYNYFUWTYEUYNYDUWS
      YEXRXPUWRUNVRVIVKUYNUWNUXGAUWFUYNUWMUXFYPUYNUWLUXEBUWFUYNUUFUXDUWKUYNUUDU
      XCUUEUYNUUCUXBYEXRYHYQXPUWRVHVIVKVOVQVOVQVSVQWAXQUXLRZXSUXMUWHUXPUXJUYIXQ
      UXLUJWBUYOUWGYAUXOUXNXRUYOYAUXNRUWGUXORXQUXLVDZYAUXNUWFWCVJUYPWDUYOUXIUYH
      DYAUXNUYOYEYAUGYEUXNUGZUXIUYHUYOYAUXNYEUYPVGUYOUXHUYGUXAUYOUXGUYFAUWFUYOY
      PUYAUXFUYEUYOYOUXTCYAUXNUYOYIYAUGYIUXNUGZYOUXTUYOYAUXNYIUYPVGUYOYKUXRYNUX
      SUYOYJUXQYHXRYEYIXQUXLVHWEYLYMXQUXLVHWFVMVNUYOUXEUYDBUWFUYOUWKUYCUXDUYOUU
      AUYBUWJYLYTXQUXLVHWGVLVQVSVQVOVMVNWHXRUXKVCUOZRZUYJUXMUXOUXNUYSUMZUWSYEUY
      SSZYERZYHUXQUYSSZYHYEUYSSZYHYIUYSSZUXLSZRZCUXNTZUWIYEUYSSZVUEYQYEUYSSZUXL
      SZRZUXBYEUYSSZYHVUKUYSSZRZUPZBUWFTZUPZAUWFTZUPZDUXNTZUQUWCUYTUXPVUAUYIVVB
      UXMUXOUXNXRUYSWIUYTUYHVVADUXNUYTUXAVUCUYGVUTUYTUWTVUBYEUWSYEXRUYSVHVKUYTU
      YFVUSAUWFUYTUYAVUIUYEVURUYTUXTVUHCUXNUYTUXRVUDUXSVUGYHUXQXRUYSVHUYTYLVUEY
      MVUFUXLYHYEXRUYSVHZYHYIXRUYSVHWJWFVQUYTUYDVUQBUWFUYTUYCVUMUXDVUPUYTUWJVUJ
      UYBVULUWIYEXRUYSVHUYTYLVUEYTVUKUXLVVCYQYEXRUYSVHZWJWFUYTUXCVUNUUEVUOUXBYE
      XRUYSVHUYTYHYHYTVUKXRUYSUYTWKUYTYHWLVVDWMWFVSVQVSVQVSVQVTUYTUXMUUOVUAUUQV
      VBUWBUXMUUOXKUYTUXLEUJEUXLOWNZWOWPVUAUUQXKZUYTUYSFRUXOUUPRUXNJRVVFFUYSPWN
      ZUWFKUXNJUWFHUKKUWEHHUWEMWNZWQLWRZUXNEUKJUXLEVVEWQQWRZWSVVJUXOUXNUUPJUYSF
      WTXAWPVVBUWBXKUYTVVAUWADUXNJUYQYEJUGVVAUWAUXNJYEVVJVAVUCUUTVUTUVTVUBUUSYE
      UWSYEUURYEUYSFUWRIUNIUWRNWNZXBDXCZVVGXDXEVUSUVSAUWFKYHUWFUGYHKUGVUSUVSUWF
      KYHVVIVAVUIUVGVURUVRVUHUVFCUXNJUYRYIJUGVUHUVFUXNJYIVVJVAVUDUVBVUGUVEYHUXQ
      YHUVAUYSFAXCZUXLEYEYIVVEXLVVGXDVUEVUFUVCUVDUXLEUYSFYHYEVVGXLZUYSFYHYIVVGX
      LVVEXDXFXGXHVUQUVQBUWFKUYMYQKUGVUQUVQUWFKYQVVIVAVUMUVLVUPUVPVUJUVIVULUVKU
      WIYEUVHYEUYSFUWEHYHYQVVHXLVVLVVGXDVUEVUKUVCUVJUXLEVVNUYSFYQYEVVGXLZVVEXDX
      FVUNUVNVUOUVOUXBYEUVMYEUYSFUWRIYHYQVVKXLVVLVVGXDYHVUKYHUVJUYSFVVMVVOVVGXD
      XFXIXGXHXIXGXHXIXGXHWPWHXJXMXN $.
      $( [12-Jul-2010] $)
  $}

  ${
    $d R w x y z $.
    vecax1.1 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    $( 1st "axiom" of a vector space or module.  The vector addition is an
       abelian group.  (Contributed by FL, 14-Sep-2010.) $)
    vecax1 $p |- ( R e. Vec -> +w e. AbelOp ) $=
      ( vw vx vz vy cvec wcel cabl c1st cfv crn c2nd cv co wceq wral wa eqid wf
      cxp cgi vecval3b simp1d ) AHIBJIAKLZKLZMZBMZUBUIANLNLZUAUFNLZUCLDOZUJPULQ
      EOZULFOZBPUJPUMULUJPZUMUNUJPBPQFUIRUMGOZUGPULUJPUOUPULUJPZBPQUMUPUKPULUJP
      UMUQUJPQSGUHRSEUHRSDUIREGFDBUJAUGUKUIUHUHTUGTUKTCUJTUITUDUE $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d R w x y z $.
    vecax2.1 $e |- X = ran ( 1st ` ( 1st ` R ) ) $.
    vecax2.5 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    vecax2.6 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    $( 2nd "axiom" of a vector space or module.  Domain, codomain and
       functionality of the multiplication of a vector by a scalar.
       (Contributed by FL, 14-Sep-2010.) $)
    vecax2 $p |- ( R e. Vec -> .w : ( X X. W ) --> W ) $=
      ( vw vx vz vy wcel cfv c1st cv co wceq wral wa eqid cvec c2nd cabl cxp wf
      cgi vecval3b simp2d ) AUALAUBMNMZUCLCBUDBDUEANMZUBMZUFMHOZDPULQIOZULJOZUI
      PDPUMULDPZUMUNDPUIPQJBRUMKOZUJNMZPULDPUOUPULDPZUIPQUMUPUKPULDPUMURDPQSKCR
      SICRSHBRIKJHUIDAUQUKBCEUQTUKTUITFGUGUH $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d R u x y z $.
    vecax3.2 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    vecax3.3 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    vecax3.4 $e |- 1t = ( Id ` ( 2nd ` ( 1st ` R ) ) ) $.
    $( 3rd "axiom" of a vector space or module.  Multiplication by 1.
       (Contributed by FL, 13-Sep-2010.) $)
    vecax3 $p |- ( R e. Vec -> A. u e. W ( 1t .w u ) = u ) $=
      ( vx vz vy wcel cv co wceq cfv c1st wral wa eqid cvec c2nd crn cxp wf cgi
      cabl w3a vecval3b opreq1i eqeq1i anbi1i ralbii sylibr simp3d simpl ralimi
      3anbi3i syl ) BUALZDAMZENZVAOZIMZVAJMZBUBPQPZNENVDVAENZVDVEENVFNOJCRVDKMZ
      BQPZQPZNVAENVGVHVAENZVFNOVDVHVIUBPZNVAENVDVKENOSKVJUCZRSIVMRZSZACRZVCACRU
      TVFUGLZVMCUDCEUEZVPUTVQVRVLUFPZVAENZVAOZVNSZACRZUHVQVRVPUHIKJAVFEBVJVLCVM
      VMTVJTVLTVFTFGUIVPWCVQVRVOWBACVCWAVNVBVTVADVSVAEHUJUKULUMURUNUOVOVCACVCVN
      UPUQUS $.
      $( [13-Sep-2010] $)
  $}

  ${
    $d R u v x y $.
    vecax4.1 $e |- X = ran ( 1st ` ( 1st ` R ) ) $.
    vecax4.4 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    vecax4.5 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    vecax4.6 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    $( 4th "axiom" of a vector space or module.  Multiplication by a scalar
       distributes over vector addition.  (Contributed by FL, 13-Sep-2010.) $)
    vecax4 $p |- ( R e. Vec ->
          A. u e. W A. x e. X
             A. v e. W
                 ( x .w ( u +w v ) ) = ( ( x .w u ) +w ( x .w v ) ) ) $=
      ( vy c1st cfv cv co wceq wral wa cvec wcel c2nd cgi cabl cxp wf crn rneqi
      eqid eqcomi eqtri vecval3b simp3d simpl ralimi adantl syl ) DUAUBZDNOZUCO
      ZUDOCPZHQVBRZAPZVBBPZGQHQVDVBHQZVDVEHQGQRBESZVDMPZUTNOZQVBHQVFVHVBHQZGQRV
      DVHVAQVBHQVDVJHQRTMFSZTZAFSZTZCESZVGAFSZCESUSGUEUBFEUFEHUGVOAMBCGHDVIVAEF
      IVIUJVAUJJKEDUCONOZUHGUHLVQGGVQJUKUIULUMUNVNVPCEVMVPVCVLVGAFVGVKUOUPUQUPU
      R $.
      $( [13-Sep-2010] $)
  $}

  ${
    $d R u x y z $.
    vecax5.1 $e |- X = ran ( 1st ` ( 1st ` R ) ) $.
    vecax5.2 $e |- +t = ( 1st ` ( 1st ` R ) ) $.
    vecax5.4 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    vecax5.5 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    vecax5.6 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    $( 5th "axiom" of a vector space or module.  Multiplication by a sum of
       scalars.  (Contributed by FL, 13-Sep-2010.) $)
    vecax5 $p |- ( R e. Vec -> A. u e. W A. x e. X A. y e. X
        ( ( x +t y ) .w u ) = ( ( x .w u ) +w ( y .w u ) ) ) $=
      ( cfv cv co wceq wral crn vz cvec wcel c1st c2nd cgi wa cabl cxp wf rneqi
      eqcomi eqtri eqid vecval3b simp3d simpl ralimi adantl syl ) DUBUCZDUDOZUE
      OZUFOCPZIQVDRZAPZVDUAPZHQIQVFVDIQZVFVGIQHQRUAESZVFBPZGQVDIQVHVJVDIQZHQRZV
      FVJVCQVDIQVFVKIQRZUGZBFSZUGZAFSZUGZCESZVLBFSZAFSZCESVAHUHUCFEUIEIUJVSABUA
      CHIDGVCEFFVBUDOZTGTJWBGGWBKULUKUMKVCUNLMEDUEOUDOZTHTNWCHHWCLULUKUMUOUPVRW
      ACEVQWAVEVPVTAFVOVTVIVNVLBFVLVMUQURUSURUSURUT $.
      $( [13-Sep-2010] $)
  $}

  ${
    $d R u x y z $.
    vecax6.1 $e |- X = ran ( 1st ` ( 1st ` R ) ) $.
    vecax6.3 $e |- .t = ( 2nd ` ( 1st ` R ) ) $.
    vecax6.5 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    vecax6.6 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    $( 6th "axiom" of a vector space or module.  Relation between scalar
       multiplication and vector multiplication.  (Contributed by FL,
       13-Sep-2010.) $)
    vecax6 $p |- ( R e. Vec -> A. u e. W A. x e. X A. y e. X
       ( ( x .t y ) .w u ) = ( x .w ( y .w u ) ) ) $=
      ( vz cfv cv co wceq c1st wral wa cvec wcel cgi c2nd cabl wf eqid vecval3b
      cxp simp3d simpr ralimi adantl syl ) DUAUBZGUCNCOZHPUPQZAOZUPMOZDUDNRNZPH
      PURUPHPZURUSHPUTPQMESZURBOZDRNRNZPUPHPVAVCUPHPZUTPQZURVCGPUPHPURVEHPQZTZB
      FSZTZAFSZTZCESZVGBFSZAFSZCESUOUTUEUBFEUIEHUFVMABMCUTHDVDGEFIVDUGJUTUGKLUH
      UJVLVOCEVKVOUQVJVNAFVIVNVBVHVGBFVFVGUKULUMULUMULUN $.
      $( [13-Sep-2010] $)
  $}

  ${
    $d +t u x y $.  $d +w u x y $.  $d .w u x y $.  $d A x y $.  $d B y $.
    $d R u x y $.  $d U u x y $.  $d W u $.  $d X u x y $.
    vecax5b.1 $e |- X = ran ( 1st ` ( 1st ` R ) ) $.
    vecax5b.2 $e |- +t = ( 1st ` ( 1st ` R ) ) $.
    vecax5b.4 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    vecax5b.5 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    vecax5b.6 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    $( Multiplication by a sum of scalars.  (Contributed by FL,
       13-Sep-2010.) $)
    vecax5b $p |- ( ( R e. Vec /\ ( U e. W /\ A e. X /\ B e. X ) ) ->
        ( ( A +t B ) .w U ) = ( ( A .w U ) +w ( B .w U ) ) ) $=
      ( vx vy vu wcel co wceq cvec cv w3a vecax5 opreq2 opreq12d eqeq12d opreq1
      wral opreq1d opreq2d rcla43v mpan9 ) CUAROUBZPUBZGSZQUBZISZUNUQISZUOUQISZ
      HSZTZPFUIOFUIQEUIDERAFRBFRUCABGSZDISZADISZBDISZHSZTZOPQCEFGHIJKLMNUDVBVHU
      PDISZUNDISZUODISZHSZTAUOGSZDISZVEVKHSZTQOPDABEFFUQDTZURVIVAVLUQDUPIUEVPUS
      VJUTVKHUQDUNIUEUQDUOIUEUFUGUNATZVIVNVLVOVQUPVMDIUNAUOGUHUJVQVJVEVKHUNADIU
      HUJUGUOBTZVNVDVOVGVRVMVCDIUOBAGUEUJVRVKVFVEHUOBDIUHUKUGULUM $.
      $( [13-Sep-2010] $)
  $}

  ${
    claddinvvec.1 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    claddinvvec.2 $e |- ~w = ( inv ` ( 1st ` ( 2nd ` R ) ) ) $.
    $( Closure of the additive inverse of a vector.  (Contributed by FL,
       13-Sep-2010.) $)
    claddinvvec $p |- ( ( R e. Vec
        /\ U e. W )
          -> ( ~w ` U ) e. W ) $=
      ( cvec wcel cfv c2nd c1st cabl wi eqid vecax1 cgr ablogrpo grpoinvcl syl
      ex imp ) AGHZBCHZBDICHZUBAJIKIZLHZUCUDMZAUEUENOUFUEPHZUGUEQUHUCUDBUEDCEFR
      TSSUA $.
      $( [13-Sep-2010] $)

    $( Double inverse law for vector additive inverse.  (Contributed by FL,
       13-Sep-2010.) $)
    vec2inv $p |- ( ( R e. Vec /\ U e. W ) -> ( ~w ` ( ~w ` U ) ) = U ) $=
      ( cvec wcel cfv wceq c2nd c1st cabl cgr wi eqid vecax1 ablogrpo grp2inv
      ex 3syl imp ) AGHZBCHZBDIDIBJZUCAKILIZMHUFNHZUDUEOAUFUFPQUFRUGUDUEBUFDCEF
      STUAUB $.
      $( [13-Sep-2010] $)
  $}


  ${
    sum2vv.1 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    sum2vv.2 $e |- W = ran +w $.
    $( The sum of two vectors is a vector.  (Contributed by FL,
       13-Sep-2010.) $)
    sum2vv $p |- ( ( R e. Vec /\ V1 e. W /\ V2 e. W )
      -> ( V1 +w V2 ) e. W ) $=
      ( cvec wcel co cabl cgr wi vecax1 ablogrpo grpocl 3exp 3syl 3imp ) AHIZCB
      IZDBIZCDEJBIZTEKIELIZUAUBUCMMAEFNEOUDUAUBUCCDEBGPQRS $.
      $( [13-Sep-2010] $)

    ${
      addnull1.1 $e |- 0w = ( Id ` +w ) $.
      $( Addition of the null vector.  (Contributed by FL, 13-Sep-2010.) $)
      addnull1 $p |- ( ( R e. Vec /\ U e. W )
        -> ( U +w 0w ) = U ) $=
        ( cvec wcel cgr co wceq cabl vecax1 ablogrpo syl grporid sylan ) AIJZDK
        JZBCJBEDLBMTDNJUAADFODPQBEDCGHRS $.
        $( [13-Sep-2010] $)


      $( Addition of the null vector.  (Contributed by FL, 13-Sep-2010.) $)
      addnull2 $p |- ( ( R e. Vec /\ U e. W )
        -> ( 0w +w U ) = U ) $=
        ( cvec wcel co wceq cabl cgr wi vecax1 ablogrpo grpolid ex 3syl imp ) A
        IJZBCJZEBDKBLZUBDMJDNJZUCUDOADFPDQUEUCUDBEDCGHRSTUA $.
        $( [13-Sep-2010] $)
    $}


    $( The addition of vectors is associative.  (Contributed by FL,
       13-Sep-2010.) $)
    addvecass $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W /\ V3 e. W ) )
       -> ( V1 +w ( V2 +w V3 ) ) = ( ( V1 +w V2 ) +w V3 ) ) $=
      ( cvec wcel w3a co wceq cabl cgr wi vecax1 ablogrpo wa grpoass eqcomd imp
      ex 3syl ) AIJZCBJDBJEBJKZCDEFLFLZCDFLEFLZMZUEFNJFOJZUFUIPAFGQFRUJUFUIUJUF
      SUHUGCDEFBHTUAUCUDUB $.
      $( [13-Sep-2010] $)


    $( The addition of vectors is associative.  (Contributed by FL,
       13-Sep-2010.) $)
    addvecom $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W ) )
       -> ( V1 +w V2 ) = ( V2 +w V1 ) ) $=
      ( cvec wcel wa co wceq cabl wi vecax1 ablocom 3expib syl imp ) AHIZCBIZDB
      IZJZCDEKDCEKLZTEMIZUCUDNAEFOUEUAUBUDCDEBGPQRS $.
      $( [13-Sep-2010] $)

    ${
      invaddvec.2 $e |- ~w = ( inv ` +w ) $.
      $( Additive inverse of a sum of vectors.  (Contributed by FL,
         13-Sep-2010.) $)
      invaddvec $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W ) )
         -> ( ~w ` ( V1 +w V2 ) ) = ( ( ~w ` V1 ) +w ( ~w ` V2 ) ) ) $=
        ( wcel wa co cfv wceq imp crn eqtri cgn claddinvvec ex cvec cabl cgr wi
        vecax1 ablogrpo grpinvop 3expib 3syl c2nd rneqi fveq2i anim12d addvecom
        c1st ancomd syldan eqtrd ) AUAJZCBJZDBJZKZKZCDELFMZDFMZCFMZELZVFVEELZUS
        VBVDVGNZUSEUBJEUCJZVBVIUDAEGUEEUFVJUTVAVICDEFBHIUGUHUIOUSVBVEBJZVFBJZKV
        GVHNVCVLVKUSVBVLVKKUSUTVLVAVKUSUTVLACBFBEPAUJMUOMZPHEVMGUKQZFERMVMRMIEV
        MRGULQZSTUSVAVKADBFVNVOSTUMOUPABVEVFEGHUNUQUR $.
        $( [13-Sep-2010] $)
    $}
  $}

  ${
    prodvs.1 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    prodvs.2 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    prodvs.3 $e |- W = ran +w $.
    prodvs.4 $e |- X = ran +t $.
    prodvs.5 $e |- +t = ( 1st ` ( 1st ` R ) ) $.
    $( The product of a vector by a scalar is a vector.  (Contributed by FL,
       12-Sep-2010.) $)
    prodvs $p |- ( ( R e. Vec /\ A e. X /\ U e. W )
      -> ( A .w U ) e. W ) $=
      ( cvec wcel crn c1st cfv rneqi eqtri cxp wf co c2nd vecax2 foprrn syl3an1
      ) BNOEDUADHUBAEOCDOACHUCDOBDEHEFPBQRQRZPLFUHMSTJDGPBUDRQRZPKGUIISTUEACDED
      HUFUG $.
      $( [12-Sep-2010] $)
  $}

  ${
    vecsrcan.1 $e |- -w = ( /g ` +w ) $.
    vecsrcan.2 $e |- W = ran +w $.
    vecsrcan.3 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    $( Right cancellation law for vector subtraction.  (Contributed by FL,
       12-Sep-2010.) $)
    vecsrcan $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W /\ V3 e. W ) ) ->
                  ( ( V1 -w V3 ) = ( V2 -w V3 ) <-> V1 = V2 ) ) $=
      ( cvec wcel w3a co wceq wb cabl cgr wi vecax1 ablogrpo grpdrcan 3syl imp
      ex ) AKLZCBLDBLEBLMZCEGNDEGNOCDOPZUFFQLFRLZUGUHSAFJTFUAUIUGUHCDEGFBIHUBUE
      UCUD $.
      $( [12-Sep-2010] $)


    $( Left cancellation law for vector subtraction.  (Contributed by FL,
       12-Sep-2010.) $)
    vecslcan $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W /\ V3 e. W ) ) ->
                  ( ( V3 -w V1 ) = ( V3 -w V2 ) <-> V1 = V2 ) ) $=
      ( cvec wcel w3a co wceq wb cabl cgr wi vecax1 ablogrpo grpdlcan 3syl imp
      ex ) AKLZCBLDBLEBLMZECGNEDGNOCDOPZUFFQLFRLZUGUHSAFJTFUAUIUGUHCDEGFBIHUBUE
      UCUD $.
      $( [12-Sep-2010] $)

  $}


  ${
    vwit.1 $e |- 0w = ( Id ` +w ) $.
    vwit.2 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    vwit.3 $e |- -w = ( /g ` +w ) $.
    vwit.4 $e |- W = ran +w $.
    $( A vector minus itself equals zero.  (Contributed by FL, 12-Sep-2010.) $)
    vwit $p |- ( ( R e. Vec /\ U e. W ) -> ( U -w U ) = 0w ) $=
      ( cvec wcel cgr co wceq cabl vecax1 ablogrpo syl grpdivid sylan ) AKLZDML
      ZBCLBBENFOUBDPLUCADHQDRSBEFDCJIGTUA $.
      $( [12-Sep-2010] $)

    ${
      vwit.5 $e |- ~w = ( inv ` +w ) $.
      $( Definition of the subtraction of two vectors.  (Contributed by FL,
         12-Sep-2010.) $)
      sub2vec $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W ) )
        -> ( V1 -w V2 ) = ( V1 +w ( ~w ` V2 ) ) ) $=
        ( cvec wcel wa cgr co cfv wceq vecax1 ablogrpo syl adantr simprl simprr
        cabl grpdivval syl111anc ) ANOZCBOZDBOZPZPEQOZUKULCDFRCDHSERTUJUNUMUJEU
        GOUNAEJUAEUBUCUDUJUKULUEUJUKULUFCDFEHBLMKUHUI $.
        $( [12-Sep-2010] $)
    $}

    $( Moving a vector from the right member of an equation into the left
       member.  (Contributed by FL, 12-Sep-2010.) $)
    mvecrtol $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W ) )
         -> ( V1 = V2 <-> ( V1 -w V2 ) = 0w ) ) $=
      ( cvec wcel wa co wceq cgr wb cabl vecax1 ablogrpo adantr simprl grpdrcan
      syl simprr syl13anc vwit adantrl eqeq2d bitr3d ) ALMZCBMZDBMZNZNZCDFOZDDF
      OZPZCDPZUQGPUPEQMZUMUNUNUSUTRULVAUOULESMVAAEITEUAUEUBULUMUNUCULUMUNUFZVBC
      DDFEBKJUDUGUPURGUQULUNURGPUMADBEFGHIJKUHUIUJUK $.
      $( [12-Sep-2010] $)


    $( Double subtraction of vectors.  (Contributed by FL, 12-Sep-2010.) $)
    dblsubvec $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W /\ V3 e. W ) )
         -> ( ( V1 -w V2 ) -w V3 ) = ( V1 -w ( V2 +w V3 ) ) ) $=
      ( wcel w3a wa co cgn cfv wceq sub2vec cvec eqid 3adantr3 simpl simpr1 crn
      opreq1d c2nd rneqi fveq2i claddinvvec 3ad2antr2 sum2vv syl111anc syl12anc
      c1st eqtri simpr3 invaddvec 3adantr1 eqcomd opreq2d 3ad2antr3 3jca syldan
      addvecass 3adant3r1 3eqtr4d 3eqtrd ) AUAMZCBMZDBMZEBMZNZOZCDGPZEGPCDFQRZR
      ZFPZEGPZVSEVQRZFPZCDEFPZGPZVOVPVSEGVJVKVLVPVSSVMABCDFGHVQIJKLVQUBZTUCUGVO
      VJVSBMZVMVTWBSVJVNUDZVOVJVKVRBMZWFWGVJVKVLVMUEZVJVKVLWHVMADBVQBFUFAUHRUPR
      ZUFLFWJJUIUQZFWJQJUJZUKULZABCVRFJLUMUNVJVKVLVMURABVSEFGHVQIJKLWETUOVOCVRW
      AFPZFPZCWCVQRZFPZWBWDVOWNWPCFVOWPWNVJVLVMWPWNSVKABDEFVQJLWEUSUTVAVBVJVNVK
      WHWABMZNZWBWOSVOVKWHWRWIWMVJVKVMWRVLAEBVQWKWLUKVCVDVJWSOWOWBABCVRWAFJLVFV
      AVEVOVJVKWCBMZWDWQSWGWIVJVLVMWTVKABDEFJLUMVGABCWCFGHVQIJKLWETUOVHVI $.
      $( [12-Sep-2010] $)

    ${
      vecrcan.1 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
      vecrcan.2 $e |- W = ran +w $.
      $( Right cancellation law for vector addition.  (Contributed by FL,
         12-Sep-2010.) $)
      vecrcan $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W /\ V3 e. W ) ) ->
                  ( ( V1 +w V3 ) = ( V2 +w V3 ) <-> V1 = V2 ) ) $=
        ( cvec wcel w3a co wceq wb cabl cgr wi vecax1 ablogrpo grporcan ex 3syl
        imp ) AOPZCBPDBPEBPQZCEFRDEFRSCDSTZUJFUAPFUBPZUKULUCAFJUDFUEUMUKULCDEFB
        LUFUGUHUI $.
        $( [12-Sep-2010] $)


      $( Left cancellation law for vector addition.  (Contributed by FL,
         12-Sep-2010.) $)
      veclcan $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W /\ V3 e. W ) ) ->
                  ( ( V3 +w V1 ) = ( V3 +w V2 ) <-> V1 = V2 ) ) $=
        ( cvec wcel w3a co wceq wb cabl cgr wi vecax1 ablogrpo grpolcan ex 3syl
        imp ) AOPZCBPDBPEBPQZECFREDFRSCDSTZUJFUAPFUBPZUKULUCAFJUDFUEUMUKULCDEFB
        LUFUGUHUI $.
        $( [12-Sep-2010] $)
    $}


    $( Moving a vector from the right member of an equation into the left
       member.  (Contributed by FL, 12-Sep-2010.) $)
    mvecrtol2 $p |- ( ( R e. Vec /\ ( V1 e. W /\ V2 e. W /\ V3 e. W ) )
         -> ( V1 = ( V2 +w V3 ) <-> ( V1 -w V2 ) = V3 ) ) $=
      ( wcel w3a wa co wceq sum2vv cgn cfv cvec cgr wb cabl vecax1 ablogrpo syl
      adantr simpr1 3adant3r1 grpdrcan syl13anc addvecom 3adantr1 opreq1d simpl
      simpr2 wi 3exp com3l impcom 3adant1 eqid sub2vec syl12anc simpr3 crn c2nd
      c1st rneqi eqtri fveq2i claddinvvec 3ad2antr2 3jca addvecass syldan simp2
      sylan2 eqcomd opreq2d 3eqtr2d vwit addnull1 3ad2antr3 eqtrd 3eqtrd eqeq2d
      jca bitr3d ) AUAMZCBMZDBMZEBMZNZOZCDGPZDEFPZDGPZQZCWRQZWQEQWPFUBMZWLWRBMZ
      WMWTXAUCWKXBWOWKFUDMXBAFJUEFUFUGUHWKWLWMWNUIWKWMWNXCWLABDEFJLRUJWKWLWMWNU
      QZCWRDGFBLKUKULWPWSEWQWPWSEDFPZDGPZEDDGPZFPZEWPWRXEDGWKWMWNWRXEQWLABDEFJL
      UMUNUOWPXFXEDFSTZTZFPZEDXJFPZFPZXHWPWKXEBMZWMXFXKQWKWOUPWOWKXNWMWNWKXNURZ
      WLWNWMXOWKWNWMXNWKWNWMXNABEDFJLRUSUTVAVBVAXDABXEDFGHXIIJKLXIVCZVDVEWKWOWN
      WMXJBMZNXMXKQWPWNWMXQWKWLWMWNVFXDWKWLWMXQWNADBXIBFVGAVHTVITZVGLFXRJVJVKFX
      RSJVLVMVNVOABEDXJFJLVPVQWPXLXGEFWPXGXLWOWKWMWMOXGXLQWOWMWMWLWMWNVRZXSWIAB
      DDFGHXIIJKLXPVDVSVTWAWBWPXHEHFPZEWPXGHEFWKWLWMXGHQWNADBFGHIJKLWCVNWAWKWLW
      NXTEQWMAEBFHJLIWDWEWFWGWHWJ $.
      $( [12-Sep-2010] $)
  $}

  ${
    prvs.1 $e |- X = ran ( 1st ` ( 1st ` R ) ) $.
    prvs.2 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    prvs.3 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    $( The product of a vector by a scalar is a vector.  (Contributed by FL,
       12-Sep-2010.) $)
    prvs $p |- ( ( R e. Vec /\ A e. X /\ U e. W ) -> ( A .w U ) e. W ) $=
      ( cvec wcel cxp wf co vecax2 foprrn syl3an1 ) BJKEDLDFMAEKCDKACFNDKBDEFGI
      HOACDEDFPQ $.
      $( [12-Sep-2010] $)
  $}


  ${
    mulveczer.1 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    mulveczer.2 $e |- 0t = ( Id ` +t ) $.
    mulveczer.3 $e |- +t = ( 1st ` ( 1st ` R ) ) $.
    mulveczer.4 $e |- .t = ( 2nd ` ( 1st ` R ) ) $.
    mulveczer.5 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    mulveczer.6 $e |- 0w = ( Id ` ( 1st ` ( 2nd ` R ) ) ) $.
    $( Multiplication of a vector by zero.  (Contributed by FL,
       12-Sep-2010.) $)
    mulveczer $p |- ( ( R e. Vec /\ <. +t , .t >. e. RingOps /\
         U e. W ) -> ( 0t .w U ) = 0w ) $=
      ( wcel co cfv c1st wceq eqid cvec cop cring w3a c2nd cgs crn fvex eqeltri
      cvv op1st eqcomi ring0cl ring0lid eqcomd mpdan 3ad2ant2 simp1 simp3 rneqi
      wa opreq1d vecax5b syl13anc eqtrd prodvs syl3an2 mvecrtol2 mpbid syl11anc
      wb vwit eqtr3d ) AUAOZDEUBZUCOZBCOZUDZFBGPZVSAUEQRQZUFQZPZVSHVRVSVSVSVTPZ
      SZWBVSSZVRVSFFDPZBGPZWCVRFWFBGVPVNFWFSZVQVPFDUGZOZWHVODWIFVORQDDEDARQZRQZ
      UJKWKRUHUIUKULZWITZJUMZVPWJVAWFFFVODWIFWMWNJUNUOUPUQVBVRVNVQFWLUGZOZWQWGW
      CSVNVPVQURZVNVPVQUSVPVNWQVQVODWPFWMWLDDWLKULUTJUMUQZWSFFABCWPDVTGWPTKVTTZ
      MIVCVDVEVRVNVSCOZXAXAWDWEVKWRVPVNWJVQXAWOFABCWIDVTGWTMIWNKVFVGZXBXBACVSVS
      VSVTWAHNWTWATZIVHVDVIVRVNXAWBHSWRXBAVSCVTWAHNWTXCIVLVJVM $.
      $( [12-Sep-2010] $)
  $}


  ${
    mulinvsca.1 $e |- X = ran +t $.
    mulinvsca.2 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    mulinvsca.3 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    mulinvsca.4 $e |- ~t = ( inv ` +t ) $.
    mulinvsca.5 $e |- ~w = ( inv ` ( 1st ` ( 2nd ` R ) ) ) $.
    mulinvsca.6 $e |- +t = ( 1st ` ( 1st ` R ) ) $.
    mulinvsca.7 $e |- .t = ( 2nd ` ( 1st ` R ) ) $.
    $( Multiplication by the inverse of a scalar.  (Contributed by FL,
       12-Sep-2010.) $)
    mulinvsca $p |- ( ( R e. Vec /\ <. +t , .t >. e. RingOps
        /\ ( A e. X /\ U e. W ) )
          -> ( ( ~t ` A ) .w U ) = ( ~w ` ( A .w U ) ) ) $=
      ( wcel cfv wceq cvec cop cring wa w3a co c2nd c1st cgs simp1 prodvs 3expb
      eqid 3adant2 jca cgi vwit simpl mulveczer 3adant3l cgr fvex eqeltri op1st
      wi eqcomi ringogrpo 3ad2ant2 simp3l grporinv eqcomd opreq1d eqtr3d adantl
      cvv syl11anc eqtrd ex syl mpcom simp3r rneqi eqtri prvs syl111anc sub2vec
      crn syl12anc rnginvcl adantrr 3adant1 vecax5b syl13anc 3eqtr3rd wb vecax1
      cabl ablogrpo 3ad2ant1 claddinvvec grpolcan mpbid ) BUARZFGUBZUCRZAERZCDR
      ZUDZUEZACIUFZAHSZCIUFZBUGSUHSZUFZXJXJJSZXMUFZTZXLXOTZXIXJXJXMUISZUFZAXKFU
      FZCIUFZXPXNXCXJDRZUDZXIXTYBTZXIXCYCXCXEXHUJZXCXHYCXEXCXFXGYCABCDEFXMIXMUM
      ZMLKPUKULUNZUOYDXTXMUPSZTZXIYEVEBXJDXMXSYIYIUMZYGXSUMZLUQYJXIYEYJXIUDXTYI
      YBYJXIURXIYIYBTYJXIFUPSZCIUFZYIYBXCXEXGYNYITXFBCDFGYMIYILYMUMZPQMYKUSUTXI
      YMYACIXIYAYMXIFVARZXFYAYMTXEXCYPXHXDFXDUHSFFGFBUHSZUHSZVOPYQUHVBVCVDVFVGV
      HXCXEXFXGVIZAYMFHEKYONVJVPVKVLVMVNVQVRVSVTXIXCYCYCXTXPTYFXIXCXFXGYCYFYSXC
      XEXFXGWAZABCDEIEFWGYRWGKFYRPWBWCZLMWDWEZUUBBDXJXJXMXSYIJYKYGYLLOWFWHXIXCX
      GXFXKERZYBXNTYFYTYSXEXHUUCXCXEXFUUCXGAFGHEKNWIWJWKZAXKBCDEFXMIUUAPYGMLWLW
      MWNXIXMVARZXLDRZXODRZYCXQXRWOXCXEUUEXHXCXMWQRUUEBXMYGWPXMWRVSWSXIXCUUCXGU
      UFYFUUDYTXKBCDEIUUALMWDWEXIXCYCUUGYFYHBXJDJLOWTVPYHXLXOXJXMDLXAWMXB $.
      $( [12-Sep-2010] $)
  $}

  ${
    $d +t u x y $.  $d .t u x y $.  $d R u x y $.  $d W x y $.  $d X y $.
    muldisc.1 $e |- X = ran ( 1st ` ( 1st ` R ) ) $.
    muldisc.2 $e |- +t = ( 1st ` ( 1st ` R ) ) $.
    muldisc.4 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    muldisc.5 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    muldisc.6 $e |- W = ran ( 1st ` ( 2nd ` R ) ) $.
    muldisc.7 $e |- -t = ( /g ` +t ) $.
    muldisc.8 $e |- -w = ( /g ` +w ) $.
    muldisc.9 $e |- .t = ( 2nd ` ( 1st ` R ) ) $.
    $( Multiplication by a difference of scalars.  (Contributed by FL,
       12-Sep-2010.) $)
    muldisc $p |- ( ( R e. Vec /\ <. +t , .t >. e. RingOps )
        -> A. u e. W A. x e. X A. y e. X
          ( ( x -t y ) .w u ) = ( ( x .w u ) -w ( y .w u ) ) ) $=
      ( cvec wcel cop cring wa cv co wceq wral cgn cfv cgr c1st crn cvv eqeltri
      fvex op1st eqcomi ringogrpo adantl ad2antrr eleq2i biimpi rneqi grpdivval
      eqid syl111anc opreq1d simplll simplrl simplrr syl5eqelr fveq2i grpoinvcl
      ad2antlr vecax5b syl13anc simpllr c2nd mulinvsca syl112anc opreq2d 3eqtrd
      sylan cabl vecax1 ablogrpo syl adantr wi prvs 3exp com3l impcom com12 imp
      com3r eqtri eqtr4d r19.21aiva ex r19.21aivv ) DUAUBZGIUCZUDUBZUEZAUFZBUFZ
      HUGZCUFZLUGZXHXKLUGZXIXKLUGZKUGZUHZBFUIZCAEFXGXKEUBZXHFUBZUEZXQXGXTUEZXPB
      FYAXIFUBZUEZXLXMXNJUJUKZUKZJUGZXOYCXLXHXIGUJUKZUKZGUGZXKLUGZXMYHXKLUGZJUG
      ZYFYCXJYIXKLYCGULUBZXHDUMUKZUMUKZUNZUBZXIYPUBZXJYIUHXGYMXTYBXFYMXDXEGXEUM
      UKGGIGYOUONYNUMUQUPURUSUTZVAVBXTYQXGYBXSYQXRXSYQFYPXHMVCVDVAVPYBYRYAYBYRF
      YPXIMVCVDVAZXHXIHGYGYPGUNYPGYONVEUSYGVGZRVFVHVIYCXDXRXSYHFUBZYJYLUHXDXFXT
      YBVJZXGXRXSYBVKZXGXRXSYBVLYAYOULUBZYBUUBXFUUEXDXTXFYOGULNYSVMVPXIYOYGFMGY
      OUJNVNVOWEXHYHDXKEFGJLMNOPQVQVRYCYKYEXMJYCXDXFYRXRYKYEUHUUCXDXFXTYBVSYTUU
      DXIDXKEYPGIYGLYDYOGGYONUSVEQPUUAJDVTUKUMUKZUJOVNNTWAWBWCWDYCJULUBZXMEUBZX
      NEUBZXOYFUHXGUUGXTYBXDUUGXFXDJWFUBUUGDJOWGJWHWIWJVBYAUUHYBXGXTUUHXDXTUUHW
      KXFXTXDUUHXSXRXDUUHWKXDXSXRUUHXDXSXRUUHXHDXKEFLMQPWLWMWNWOWPWJWQWJYAYBUUI
      XTXGYBUUIWKZXRXGUUJWKXSXGYBXRUUIXDYBXRUUIWKWKXFXDYBXRUUIXIDXKEFLMQPWLWMWJ
      WRWJWOWQXMXNKJYDEEUUFUNJUNQUUFJJUUFOUSVEWSYDVGSVFVHWTXAXBXC $.
      $( [12-Sep-2010] $)
  $}

  ${
    $d +t u x y $.  $d -t u x y $.  $d -w u x y $.  $d .t u x y $.
    $d .w u x y $.  $d A x y $.  $d B y $.  $d R u x y $.  $d U u x y $.
    $d W u x y $.  $d X u x y $.
    vecax5c.1 $e |- X = ran +t $.
    vecax5c.2 $e |- +t = ( 1st ` ( 1st ` R ) ) $.
    vecax5c.3 $e |- -t = ( /g ` +t ) $.
    vecax5c.4 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    vecax5c.5 $e |- -w = ( /g ` +w ) $.
    vecax5c.6 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    vecax5c.7 $e |- W = ran +w $.
    vecax5c.8 $e |- .t = ( 2nd ` ( 1st ` R ) ) $.
    $( Multiplication by a difference of scalars.  (Contributed by FL,
       12-Sep-2010.) $)
    vecax5c $p |- ( ( R e. Vec /\ <. +t , .t >. e. RingOps ) -> (
        ( U e. W /\ A e. X /\ B e. X ) ->
          ( ( A -t B ) .w U ) = ( ( A .w U ) -w ( B .w U ) ) ) ) $=
      ( vx vy vu cvec wcel cop cring wa cv co wceq wral w3a crn cfv rneqi eqtri
      c1st c2nd muldisc opreq2 opreq12d eqeq12d opreq1d opreq2d rcla43v syl5com
      opreq1 ) CUDUEGIUFUGUEUHUAUIZUBUIZHUJZUCUIZLUJZVIVLLUJZVJVLLUJZKUJZUKZUBF
      ULUAFULUCEULDEUEAFUEBFUEUMABHUJZDLUJZADLUJZBDLUJZKUJZUKZUAUBUCCEFGHIJKLFG
      UNCURUOURUOZUNMGWDNUPUQNPREJUNCUSUOURUOZUNSJWEPUPUQOQTUTVQWCVKDLUJZVIDLUJ
      ZVJDLUJZKUJZUKAVJHUJZDLUJZVTWHKUJZUKUCUAUBDABEFFVLDUKZVMWFVPWIVLDVKLVAWMV
      NWGVOWHKVLDVILVAVLDVJLVAVBVCVIAUKZWFWKWIWLWNVKWJDLVIAVJHVHVDWNWGVTWHKVIAD
      LVHVDVCVJBUKZWKVSWLWBWOWJVRDLVJBAHVAVDWOWHWAVTKVJBDLVHVEVCVFVG $.
      $( [12-Sep-2010] $)
  $}

  ${
    $d +t k s u y $.  $d +w k s $.  $d .t k $.  $d .w s $.  $d 0t s $.
    $d 0w k s $.  $d N k s u y $.  $d R k $.  $d S1 s u y $.  $d S2 s u y $.
    $d U s $.  $d W k $.  $d X k s y $.
    svli2.1 $e |- X = ran +t $.
    svli2.2 $e |- 0t = ( Id ` +t ) $.
    svli2.7 $e |- +t = ( 1st ` ( 1st ` R ) ) $.
    svli2.3 $e |- .t = ( 2nd ` ( 1st ` R ) ) $.
    svli2.4 $e |- 0w = ( Id ` +w ) $.
    svli2.5 $e |- +w = ( 1st ` ( 2nd ` R ) ) $.
    svli2.6 $e |- W = ran +w $.
    svli2.8 $e |- .w = ( 2nd ` ( 2nd ` R ) ) $.
    $( If a finite sequence of vectors ` U ( k ) ` are linearly independant,
       two combinations of those vectors are equal iff the scalars are equal.
       (Contributed by FL, 9-Nov-2010.) $)
    svli2 $p |-
       ( ( ( R e. Vec
                /\ <. +t , .t >. e. RingOps
                /\ N e. NN )
         /\ (
                   A. k e. ( 1 ... N ) U e. W
                /\ A. k e. ( 1 ... N ) S1 e. X
                /\ A. k e. ( 1 ... N ) S2 e. X )
         /\ A. s e. ( X ^m ( 1 ... N ) )
                  ( prod_ k e. ( 1 ... N ) +w ( ( s ` k ) .w U ) = 0w
                           -> A. k e. ( 1 ... N ) ( s ` k ) = 0t ) )
        -> (
               prod_ k e. ( 1 ... N ) +w ( S1 .w U ) =
               prod_ k e. ( 1 ... N ) +w ( S2 .w U )
                   <-> A. k e. ( 1 ... N ) S1 = S2 ) ) $=
      ( vy vu cvec wcel cop cring cn w3a c1 cfz co wral cfv cprd2 wceq cmap cgs
      cv wi wa wb simp11 cuz cabl elnnuz biimpi 3ad2ant3 adantr vecax1 3ad2ant1
      r19.26 prodvs 3exp com13 com12 ralimdv 3adant3 clfsebs5 syl111anc 3adant2
      imp sylbir jca eqid mvecrtol syl11anc sylanbrc fprodsub eqcomd syl eqeq1d
      3jca r19.26-3 vecax5c ex syl5bir cvv c2nd c1st fvex eqeltri prodeq2 copab
      wf op1st eqcomi ringogrpo grpdivcl 3expib 3adant1 syl5com 3ad2ant2 fopab2
      cgr sylib crn rnex oprex elmap sylibr wel ax-17 hbopab1 fveq1 opreq1d a1d
      hbeq r19.21ai ralbid imbi12d fvopab2 mpan2 mprg eqtri cgi ad2antrr 3bitrd
      eqeq1i ralbiia imbi12i syl6bb rcla4v opreq1 ralimi adantl rneqi mulveczer
      fveq2i 3expia cmagm cexid cin cmnd ablogrpo grpmnd mndmgmid 3eqtrd impbid
      3syl fincmpzer mpdan grpdivzer com3l ralbi syl6com impcom ) AUFUGZJKUHZUI
      UGZDUJUGZUKZBEUGZCULDUMUNZUOZHFUGZCUVPUOZIFUGZCUVPUOZUKZUVPCVAZGVAZUPZBNU
      NZCMUQZOURZUWELURZCUVPUOZVBZGFUVPUSUNZUOZUKZUVPHBNUNZCMUQZUVPIBNUNZCMUQZU
      RZUWPUWRMUTUPZUNZOURZUVPUWOUWQUWTUNZCMUQZOURZHIURZCUVPUOZUWNUVJUWPEUGZUWR
      EUGZVCZUWSUXBVDUVJUVLUVMUWBUWMVEUVNUWBUXJUWMUVNUWBVCZUXHUXIUXKDULVFUPUGZM
      VGUGZUWOEUGZCUVPUOZUXHUVNUXLUWBUVMUVJUXLUVLUVMUXLDVHVIVJZVKZUVNUXMUWBUVJU
      VLUXMUVMAMUAVLZVMVKZUVNUWBUXOUVJUVLUWBUXOVBUVMUWBUVJUXOUVQUVSUVJUXOVBZUWA
      UVQUVSVCUVOUVRVCZCUVPUOZUXTUVOUVRCUVPVNUVJUYBUXOUVJUYAUXNCUVPUYAUVJUXNUVO
      UVRUVJUXNVBUVJUVRUVOUXNUVJUVRUVOUXNHABEFJMNUAUCUBPRVOVPVQWDVRVSVRWEVTVRVM
      WDZUWOCMULDEUBWAWBUXKUXLUXMUWQEUGZCUVPUOZUXIUXQUXSUVNUWBUYEUVJUVLUWBUYEVB
      UVMUWBUVJUYEUVQUWAUVJUYEVBZUVSUVQUWAVCUVOUVTVCZCUVPUOZUYFUVOUVTCUVPVNUVJU
      YHUYEUVJUYGUYDCUVPUYGUVJUYDUVOUVTUVJUYDVBUVJUVTUVOUYDUVJUVTUVOUYDIABEFJMN
      UAUCUBPRVOVPVQWDVRVSVRWEWCVRVMWDZUWQCMULDEUBWAWBWFVTAEUWPUWRMUWTOTUAUWTWG
      ZUBWHWIUWNUXAUXDOUWNUXLUXNUYDVCCUVPUOZUXMUKZUXAUXDURUVNUWBUYLUWMUXKUXLUYK
      UXMUXQUXKUXOUYEUYKUYCUYIUXNUYDCUVPVNWJUXSWOVTUYLUXDUXAUWOUWQUWTCMULDEUBUY
      JWKWLWMWNUWNUXEUVPHIJUTUPZUNZBNUNZCMUQZOURZUYNLURZCUVPUOZUXGUWNUXDUYPOUWN
      UXCUYOURZCUVPUOZUXDUYPURUVNUWBVUAUWMUVNUWBVUAUWBUVOUVRUVTUKZCUVPUOZUVNVUA
      UVOUVRUVTCUVPWPZUVNVUBUYTCUVPUVNVUBUYTUVNVUBVCUYOUXCUVNVUBUYOUXCURZUVJUVL
      VUBVUEVBUVMHIABEFJUYMKMUWTNPRUYMWGZUAUYJUCUBSWQVTWDWLWRVSWSWDVTUVPUXCUYOW
      TCMMAXAUPZXBUPZWTUAVUGXBXCXDZXEWMWNUWNUWCUVPUGZUDVAUYNURVCZCUDXFZUWLUGZUY
      QUYSVDUVNUWBVUMUWMUXKUVPFVULXGZVUMUXKUYNFUGZCUVPUOZVUNUVNUWBVUPUVLUVJUWBV
      UPVBUVMUVLJXQUGZUWBVUPUVKJUVKXBUPJJKJAXBUPZXBUPWTRVURXBXCXDZXHXIXJZUVSUWA
      VUQVUPVBZUVQUVSUWAVCUVRUVTVCZCUVPUOZVVAUVRUVTCUVPVNVUQVVCVUPVUQVVBVUOCUVP
      VUQUVRUVTVUOHIUYMJFPVUFXKXLVSVRWEXMXNXOWDCUDUVPFUYNVULVULWGXPXRFUVPVULFJX
      SWTPJVUSXTXDULDUMYAYBYCVTUWNVUMVCZUYQUYSUWNVUMUYQUYSVBZUWMUVNVUMVVEVBUWBV
      UMUWMVVEUWKVVEGVULUWLUWDVULURZUWKUVPUWCVULUPZBNUNZCMUQZOURZVVGLURZCUVPUOZ
      VBVVEVVFUWHVVJUWJVVLVVFUWGVVIOVVFUWFVVHURZCUVPUOUWGVVIURVVFVVMCUVPCUEUEUW
      DVULUEGYDCYEVUKCUDUEYFYJZVVFVVMVUJVVFUWEVVGBNUWCUWDVULYGZYHYIYKUVPUWFVVHW
      TCMVUIXEWMWNVVFUWIVVKCUVPVVNVVFUWEVVGLVVOWNYLYMVVJUYQVVLUYSVVIUYPOVVHUYOU
      RVVIUYPURCUVPUVPVVHUYOWTCMVUIXEVUJVVGUYNBNVUJUYNWTUGVVGUYNURHIUYMYACUDUVP
      UYNWTYNYOZYHYPUUAVVKUYRCUVPVUJVVGUYNLVVPWNUUBUUCUUDUUEVRVJWDVVDUYSUYQVVDU
      YSVCUYPUVPLBNUNZCMUQZUVPOCMUQZOUYSUYPVVRURZVVDUYSUYOVVQURZCUVPUOVVTUYRVWA
      CUVPUYNLBNUUFUUGUVPUYOVVQWTCMVUIXEWMUUHUWNVVRVVSURZVUMUYSUVNUWBVWBUWMUXKV
      VQOURZCUVPUOZVWBUVNUWBVWDUVJUVLUWBVWDVBUVMUWBUVJUVLVCZVWDUVQUVSVWEVWDVBUW
      AVWEUVQVWDVWEUVOVWCCUVPUVJUVLUVOVWCABEJKLNOEMXSVUHXSUBMVUHUAUUIYQQRSUCOMY
      RUPVUHYRUPTMVUHYRUAUUKYQUUJUULVSVRVMVRVTWDUVPVVQOWTCMVUIXEWMVTYSUWNVVSOUR
      ZVUMUYSUVNUWBVWFUWMUVNUXLMUUMUUNUUOUGZVWFUXPUVJUVLVWGUVMUVJUXMMUUPUGZVWGU
      XRUXMMXQUGVWHMUUQMUURWMMUUSUVBVMOCMULDTUVCWIVMYSUUTWRUVAUVDUVNUWBUYSUXGVD
      ZUWMUWBUVNVWIUWBVUCUVNVWIVBVUDUVNVUCUYRUXFVDZCUVPUOZVWIUVLUVJVUCVWKVBZUVM
      UVLVUQVWLVUTVUQVUBVWJCUVPVUBVUQVWJUVRUVTVUQVWJVBZUVOUVRUVTVWMVUQUVRUVTVWJ
      VUQUVRUVTVWJHIUYMLJFPQVUFUVEVPUVFWDXMVRVSWMXOUYRUXFCUVPUVGUVHWEUVIVTYTYT
      $.
      $( [9-Nov-2010] $)
  $}

  $c SubVec $.

  $( Extend class notation with the class of all generic subspace vector spaces
     and modules. $)
  csvec $a class SubVec $.

  ${
    $d x y v w $.
    $( A sub-vector space ` v ` of a vector space ` x ` is a vector space that
       has the same scalar set than ` x ` , whose addition and whose
       multiplication are restrictions of those of ` x ` . $)
    df-svs $a |- SubVec = { <. x , y >. | ( x e. Vec /\
       y = { v e. Vec | ( ( 1st ` v ) = ( 1st ` x ) /\
       ( 1st ` ( 2nd ` v ) ) C_ ( 1st ` ( 2nd ` x ) ) /\
       ( 2nd ` ( 2nd ` v ) ) = ( ( 2nd ` ( 2nd ` x ) ) |`
          ( ran ( 1st ` ( 1st ` x ) ) X. ran ( 1st ` ( 2nd ` v ) ) ) ) ) } ) }
        $.

    $( A textbook definition.  A sub-vector space of a vector space ` x ` is a
       subset that is itself a vector space under the inherited operations.
       (Contributed by FL, 31-Dec-2010.) $)
    svs2 $p |- SubVec = { <. x , y >. | ( x e. Vec /\
       y = { v e. Vec | ( ( 1st ` v ) = ( 1st ` x ) /\
         E. w e. ~P ran ( 1st ` ( 2nd ` x ) )
         ( ( 1st ` ( 2nd ` v ) ) = ( ( 1st ` ( 2nd ` x ) ) |` ( w X. w ) ) /\
           ( 2nd ` ( 2nd ` v ) ) = ( ( 2nd ` ( 2nd ` x ) ) |`
             ( ran ( 1st ` ( 1st ` x ) ) X. w ) ) ) ) } ) } $=
      ( cvec wcel c1st cfv wceq wss cxp cres w3a wa wi cdm ex eqtr syl 3syl crn
      csvec cv c2nd crab copab cpw wrex df-svs rnss fvex rnex elpw biimpri cabl
      3anass cgr eqid vecax1 ablogrpo wfun grpfo funssres dmrngrp reseq2 eqcomd
      wfo fofun eqcoms syl9 imp adantr 3adant3 jca xpeq12 anidms reseq2d eqeq2d
      simp3 xpeq2 anbi12d rcla4ev syl5com 3exp com14 pm2.43i com12 resss mpbiri
      sseq1 3ad2ant2 dmxpid visset xpss12 sseq2 ssdmres idd com13 com3l sylancl
      dmeq a1i sylbi syl6bi com24 3imp sylancb impcom biimpd r19.23aiv pm5.32da
      impbid syl5bb rabbidva pm5.32i opabbii eqtri ) UBAUCZEFZBUCZDUCZGHXRGHZIZ
      YAUDHZGHZXRUDHZGHZJZYDUDHZYFUDHZYBGHUAZYEUAZKZLZIZMZDEUEZIZNZABUFXSXTYCYE
      YGCUCZYTKZLZIZYIYJYKYTKZLZIZNZCYGUAZUGZUHZNZDEUEZIZNZABUFABDUIYSUUNABXSYR
      UUMXSYQUULXTXSYPUUKDEYPYCYHYONZNXSYAEFZNZUUKYCYHYOUPUUQYCUUOUUJUUQYCNZUUO
      UUJUUOUURUUJYHYOUURUUJOZYHYOUUSOZYHYLUUHJZYLUUIFZYHUUTOYEYGUJUVBUVAYLUUHY
      EYDGUKULUMUNUURYHYOUVBUUJUURYHYOUVBUUJOUURYHYOMZYEYGYLYLKZLZIZYONZUVBUUJU
      VCUVFYOUURYHUVFYOUURYHUVFUUQYHUVFOZYCXSUUPUVHXSYGUOFZYGUQFZUUPUVHOZXRYGYG
      URUSZYGUTZUVJUUHUUHKZUUHYGVGYGVAZUVKYGUUHUUHURVBUVNUUHYGVHUVOYHYGYEPZLZYE
      IZUUPUVFUVOYHUVRYGYEVCQUUPYEUOFZYEUQFZUVRUVFOZYAYEYEURUSZYEUTZUVTUVPUVDIZ
      UWAYEVDZUWDUVQUVEIUWAUVPUVDYGVEUWAUVEUVQUVEUVQIZUVRUVFUWFUVRNUVEYEUVEUVQY
      ERVFQVISSTVJTTVKVLVKVMUURYHYOVSVNUVBUVGUUJUUGUVGCYLUUIYTYLIZUUCUVFUUFYOUW
      GUUBUVEYEUWGUUAUVDYGUWGUUAUVDIYTYLYTYLVOVPVQVRUWGUUEYNYIUWGUUDYMYJYTYLYKV
      TZVQVRWAWBQWCWDWETWFVKWGUUJUURUUOUUGUURUUOOCUUIYTUUIFZUUGUURUUOUWIUUGUURM
      YHYOUUGUWIYHUURUUCYHUUFUUCYHUUBYGJYGUUAWHYEUUBYGWJWIVLWKUWIUUGUURYOUUGUWI
      UURYOOZUUCUUFUWIUWJOUUCUURUWIUUFYOUUCUURUWIUUFYOOUUCUURUWIMZUUFYOUWKUUEYN
      YIUWKUUDYMYJUWKUWGUUDYMIUUCUURUWIUWGUURUUCUWIUWGOZUUQYCUUCUWLOZUUPXSYCUWM
      OZUUPXSUWNOZUUPUVSUVTUUPUWOOZUWBUWCUVTUWDUWPUWEUWDUVPPZUVDPZIZUWRYLIZUWPU
      VPUVDXAYLWLUWSUWTNUWQYLIZUWPUWQUWRYLRXSUUPUXAUWNXSUVIUVJUUPUXAUWNOOZUVLUV
      MUVJYGPZUVNIZUXBYGVDUXDUUPUXAUWNUXDUUPUXAMZYCUUCUWLUWIUXEYCUUCMZUWGUWIYTU
      UHJZUXGUXFUWGOYTUUHCWMUMZUXHUXGUXGNUUAUVNJZUXFUWGYTUUHYTUUHWNUXEYCUUCUXIU
      WGOUXEUXIUUCYCUWGUXDUUPUXAUXIUUCYCUWGOZOZOUXDUXIUXAUUPUXKUXIUXAUUPUXKOOZO
      UVNUXCUVNUXCIUXIUUAUXCJZUXLUVNUXCUUAWOUXMUUBPZUUAIZUXLUUAYGWPUXOUXNPZUUAP
      ZIZUXQYTIZUXLUXNUUAXAYTWLUXRUXSNUXPYTIZUXLUXPUXQYTRUUCUXAUUPUXTUXJUUCUVPU
      XNIUWQUXPIUXAUUPUXTUXJOOZOZYEUUBXAUVPUXNXAUYBUXPUWQUXPUWQIZUXAUYAUYCUXANU
      XPYLIZUYAUXPUWQYLRUXTUYDUUPUXJUYDUUPUXJOZOYTUXPYTUXPIZUYDUYEUYFUYDNUWGUYE
      YTUXPYLRYCUUPUWGUWGUUPUWGUWGOOYCUUPUWGWQXBWRSQVIWSSQVITWESWTXCXDVIXEXFXEX
      FWCXGWGWDWDSTWRSWTSTWFXHVKWGXFUWHSVQVRXIWDXEVKWGXFVNWDXJWGXLXKXMXNVRXOXPX
      Q $.
      $( [31-Dec-2010] $)

    $( A very concise definition of a subspace of a vector space.  (Contributed
       by FL, 30-Dec-2010.) $)
    svs3 $p |- SubVec = { <. x , y >. | ( x e. Vec /\
       y = { v e. Vec | ( ( 1st ` v ) = ( 1st ` x ) /\
       ( 1st ` ( 2nd ` v ) ) C_ ( 1st ` ( 2nd ` x ) ) /\
       ( 2nd ` ( 2nd ` v ) ) C_ ( 2nd ` ( 2nd ` x ) ) ) } ) } $=
      ( cv cvec wcel c1st cfv wceq c2nd wss crn cxp cres w3a crab wa eqid syl
      wi csvec copab df-svs resss sseq1 mpbiri cdm vecax2 fdm eqcomd a1d adantl
      wf fveq2 rneqd eqcoms eqeq1d imbi2d syl5ib impcom imp adantr reseq2d wfun
      xpeq1 ffun ad2antrr funssres sylan eqtr2d impbid2 pm5.32da 3anass 3bitr4g
      ex rabbidva eqeq2d pm5.32i opabbii eqtri ) UAADZEFZBDZCDZGHZWAGHZIZWDJHZG
      HZWAJHZGHZKZWHJHZWJJHZWFGHZLZWILZMZNZIZOZCEPZIZQZABUBWBWCWGWLWMWNKZOZCEPZ
      IZQZABUBABCUCXDXIABWBXCXHWBXBXGWCWBXAXFCEWBWDEFZQZWGWLWTQZQWGWLXEQZQXAXFX
      KWGXLXMXKWGQZWLWTXEXNWLQZWTXEWTXEWSWNKWNWRUDWMWSWNUEUFXOXEWTXOXEQZWSWNWMU
      GZNZWMXPWRXQWNXOWRXQIZXEXNWLXSWGXKWLXSTZXKXTTWFWEXKWLWEGHZLZWQMZXQIZTZWFW
      EIZXTXJYEWBXJYDWLXJYCWQWMUMZYDWDWQYBWMYBRWMRWQRUHYGXQYCYCWQWMUIUJSUKULYFY
      DXSWLYFYCWRXQYFYBWPIZYCWRIYHWEWFWGYAWOWEWFGUNUOUPYBWPWQVESUQURUSUPUTVAVBV
      CXOWNVDZXEXRWMIXKYIWGWLWBYIXJWBWPWKLZMZYJWNUMYIWAYJWPWNWPRWNRYJRUHYKYJWNV
      FSVBVGWNWMVHVIVJVOVKVLVLWGWLWTVMWGWLXEVMVNVPVQVRVSVT $.
      $( [30-Dec-2010] $)

  $}

