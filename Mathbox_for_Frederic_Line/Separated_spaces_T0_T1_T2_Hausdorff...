$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Separated spaces: T0, T1, T2 (Hausdorff) ...
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.
    dtopcl.1 $e |- A e. _V $.
    $( The open sets of a discrete topology are closed and its closed sets are
       open. $)
    dtopcl $p |- ~P A = ( Clsd ` ~P A ) $=
      ( vx cpw ccld cfv ctop wcel cv wb distop cuni wss cdif wa eqid iscld
      unipw sseq2i biimpi elpw2 sylibr adantr elssuni difss sseqtri mpbir
      jctir impbii syl6rbb ax-mp eqriv ) CADZUMEFZUMGHZCIZUMHZUPUNHZJABKUOURUPU
      MLZMZUSUPNZUMHZOZUQUPUMUSUSPQVCUQUTUQVBUTUPAMZUQUTVDUSAUPARZSTUPABUAUBUCU
      QUTVBUPUMUDVBVAAMVAUSAUSUPUEVEUFVAABUAUGUHUIUJUKUL $.
      $( [7-Jun-2007] $)
  $}

  ${
    $d J x $.
    $( A Hausdorff space is a T1 space. $)
    t2t1 $p |- ( J e. Haus -> J e. Fre ) $=
      ( vx cha wcel ctop cv csn ccld cfv cuni wral wa ct1 haustop eqid sncld
      r19.21aiva jca ist1 sylibr ) ACDZAEDZBFZGAHIDZBAJZKZLAMDUAUBUFANUAUDBUEUC
      AUEUEOZPQRAUEBUGST $.
      $( [11-Jun-2007] $)
  $}

  $( A Hausdorff space is a T1 space. $)
  hst1 $p |- Haus C_ Fre $=
    ( vx cha ct1 wss cv wcel wi dfss2 t2t1 mpgbir ) BCDAEZBFKCFGAABCHKIJ $.
    $( [18-Jun-2007] $)

  ${
    $d A u v x y $.
    dtt2.1 $e |- A e. _V $.
    $( A discrete topology is Hausdorff.  Morris, Topology without tears, p.72,
       ex. 13. $)
    dtt2 $p |- ~P A e. Haus $=
      ( vx vy vu vv cpw cha wcel ctop cv wne cin c0 wceq w3a wrex wi cuni wral
      eqid ishaus distop wa csn visset snid a1i disjsn2 3jca eleq2 ineq1
      eqeq1d 3anbi13d imbi2d ineq2 3anbi23d rcla42ev 3expia unipw eleq2i
      biimpi snelpw sylib syl2an mpi r19.37av reximi 3syl rgen2a mpbir2an ) AGZ
      HIVLJICKZDKZLZVMEKZIZVNFKZIZVPVRMZNOZPZFVLQZEVLQRZDVLSZTCWETCDFEVLWEWEUAU
      BABUCWDCDWEVMWEIZVNWEIZUDZVOWBRZFVLQZEVLQZVOWCRZEVLQWDWHVOVMVMUEZIZVNVNUE
      ZIZWMWOMZNOZPZRZWKVOWNWPWRWNVOVMCUFZUGUHWPVOVNDUFZUGUHVMVNUIUJWMVLIZWOVLI
      ZWTWKRWFWGXCXDWTWKWIWTVOWNVSWMVRMZNOZPZREFWMWOVLVLVPWMOZWBXGVOXHVQWNWAXFV
      SVPWMVMUKXHVTXENVPWMVRULUMUNUOVRWOOZXGWSVOXIVSWPXFWRWNVRWOVNUKXIXEWQNVRWO
      WMUPUMUQUOURUSWFVMAIZXCWFXJWEAVMAUTZVAVBVMAXAVCVDWGVNAIZXDWGXLWEAVNXKVAVB
      VNAXBVCVDVEVFWJWLEVLVOWBFVLVGVHVOWCEVLVGVIVJVK $.
      $( [24-Jun-2007] $)
  $}

  ${
    dtt1.1 $e |- A e. _V $.
    $( A discrete topology is T1.  Morris, Topology without tears. $)
    dtt1 $p |- ~P A e. Fre $=
      ( cpw cha wcel ct1 dtt2 t2t1 ax-mp ) ACZDEJFEABGJHI $.
      $( [8-Jun-2007] $)
  $}

