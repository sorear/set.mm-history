$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Separated spaces: T0, T1, T2 (Hausdorff) ...
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.
    dtopcl.1 $e |- A e. _V $.
    $( The open sets of a discrete topology are closed and its closed sets are
       open.  (Contributed by FL, 7-Jun-2007.) $)
    dtopcl $p |- ~P A = ( Clsd ` ~P A ) $=
      ( vx cpw ccld cfv ctop wcel cv wb distop cuni wss cdif iscld unipw sseq2i
      wa eqid elpw2 biimpi sylibr adantr elssuni difss mpbir jctir impbii ax-mp
      sseqtri syl6rbb eqriv ) CADZUMEFZUMGHZCIZUMHZUPUNHZJABKUOURUPUMLZMZUSUPNZ
      UMHZRZUQUPUMUSUSSOVCUQUTUQVBUTUPAMZUQUTVDUSAUPAPZQUAUPABTUBUCUQUTVBUPUMUD
      VBVAAMVAUSAUSUPUEVEUJVAABTUFUGUHUKUIUL $.
      $( [18-Jun-2007] $) $( [7-Jun-2007] $)
  $}

  ${
    $d J x $.
    $( A Hausdorff space is a T1 space.  (Contributed by FL, 11-Jun-2007.) $)
    t2t1 $p |- ( J e. Haus -> J e. Fre ) $=
      ( vx cha wcel ctop cv csn ccld cfv cuni wral haustop eqid sncld ralrimiva
      wa ct1 jca ist1 sylibr ) ACDZAEDZBFZGAHIDZBAJZKZPAQDUAUBUFALUAUDBUEUCAUEU
      EMZNORAUEBUGST $.
      $( [18-Jun-2007] $) $( [11-Jun-2007] $)
  $}

  $( A Hausdorff space is a T1 space.  (Contributed by FL, 18-Jun-2007.) $)
  hst1 $p |- Haus C_ Fre $=
    ( vx cha ct1 wss cv wcel wi dfss2 t2t1 mpgbir ) BCDAEZBFKCFGAABCHKIJ $.
    $( [20-Jul-2007] $) $( [18-Jun-2007] $)

  ${
    $d A u v x y $.
    dtt2.1 $e |- A e. _V $.
    $( A discrete topology is Hausdorff.  Morris, Topology without tears, p.72,
       ex. 13.  (Contributed by FL, 24-Jun-2007.) $)
    dtt2 $p |- ~P A e. Haus $=
      ( vx vy vu vv wcel cv cin c0 wceq w3a wrex wi wral csn visset snid eleq2i
      a1i cpw cha ctop wne cuni eqid ishaus distop wa disjsn2 3jca unipw biimpi
      snelpw sylib eleq2 eqeq1d 3anbi13d imbi2d 3anbi23d rcla42ev 3expia syl2an
      ineq1 ineq2 mpi r19.37av reximi 3syl rgen2a mpbir2an ) AUAZUBGVLUCGCHZDHZ
      UDZVMEHZGZVNFHZGZVPVRIZJKZLZFVLMZEVLMNZDVLUEZOCWEOCDFEVLWEWEUFUGABUHWDCDW
      EVMWEGZVNWEGZUIZVOWBNZFVLMZEVLMZVOWCNZEVLMWDWHVOVMVMPZGZVNVNPZGZWMWOIZJKZ
      LZNZWKVOWNWPWRWNVOVMCQZRTWPVOVNDQZRTVMVNUJUKWFWMVLGZWOVLGZWTWKNWGWFVMAGZX
      CWFXEWEAVMAULZSUMVMAXAUNUOWGVNAGZXDWGXGWEAVNXFSUMVNAXBUNUOXCXDWTWKWIWTVOW
      NVSWMVRIZJKZLZNEFWMWOVLVLVPWMKZWBXJVOXKVQWNWAXIVSVPWMVMUPXKVTXHJVPWMVRVDU
      QURUSVRWOKZXJWSVOXLVSWPXIWRWNVRWOVNUPXLXHWQJVRWOWMVEUQUTUSVAVBVCVFWJWLEVL
      VOWBFVLVGVHVOWCEVLVGVIVJVK $.
      $( [14-Jul-2007] $) $( [24-Jun-2007] $)
  $}

  ${
    dtt1.1 $e |- A e. _V $.
    $( A discrete topology is T1.  Morris, Topology without tears.
       (Contributed by FL, 8-Jun-2007.) $)
    dtt1 $p |- ~P A e. Fre $=
      ( cpw cha wcel ct1 dtt2 t2t1 ax-mp ) ACZDEJFEABGJHI $.
      $( [18-Jun-2007] $) $( [8-Jun-2007] $)
  $}

