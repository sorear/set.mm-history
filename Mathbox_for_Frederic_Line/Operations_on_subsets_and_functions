$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Operations on subsets and functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c cset $.

  $( Extend class notation with an operator that derives an operation on
     subsets of a set from an operation on the elements of this set. $)
  ccst $a class cset $.

  ${
    $d g h x y z a u v $.
    $( Define an operation ` h ` on the subsets derived from an operation ` g `
       on the elements.  Meaningful if ` g ` is a binary internal operation. $)
    df-cst $a |- cset = { <. g , h >. | h = { <. <. x , y >. , z >. |
     ( x e. ~P dom dom g /\ y e. ~P dom dom g
          /\ z = { a | E. u e. x E. v e. y a = ( u g v ) } ) } } $.
  $}

  ${
    iscst1.1 $e |- X = dom dom G $.
    iscst1.2 $e |- H = ( cset ` G ) $.
    ${
      $d G a g h u v x y z $.  $d X g h x y z $.
      $( An operation on the subsets derived from an operation on the
         elements.  (Contributed by FL, 18-Apr-2010.) $)
      iscst1 $p |- ( G e. A -> H = { <. <. x , y >. , z >. |
        ( x e. ~P X /\ y e. ~P X
           /\ z = { a | E. u e. x E. v e. y a = ( u G v ) } ) } ) $=
        ( vg vh wcel ccst cv wceq wrex cvv cfv cpw co cab w3a copab2 elex dmexg
        wa cdm syl syl5eqel pwexg jca df-3an oprabbii oprabex2g dmeq dmeqd pweq
        ax-mp syl6eqr eleq2d opreq eqeq2d abbidv 3anbi123d oprabbidv copab cres
        rexbidv wrel relopab resid df-cst syl6reqr resopab eqtri syl2anc syl5eq
        fvopab4g ) GFOZHGPUAZAQZIUBZOZBQZWEOZCQZJQZEQZDQZGUCZRZDWGSZEWDSZJUDZRZ
        UEZABCUFZLWBGTOWTTOZWCWTRGFUGWBWETOZXBUIZXAWBITOZXCWBIGUJZUJZTKWBXETOXF
        TOGFUHXETUHUKULXDXBXBITUMZXGUNUKABCWEWEWQTTWTWSWFWHUIWRUIABCWFWHWRUOUPU
        QUKMNGWDMQZUJZUJZUBZOZWGXKOZWIWJWKWLXHUCZRZDWGSZEWDSZJUDZRZUEZABCUFZWTT
        TPXHGRZXTWSABCYBXLWFXMWHXSWRYBXKWEWDYBXKXFUBZWEYBXJXFRXKYCRYBXIXEXHGURU
        SXJXFUTUKIXFRWEYCRKIXFUTVAVBZVCYBXKWEWGYDVCYBXRWQWIYBXQWPJYBXPWOEWDYBXO
        WNDWGYBXNWMWJWKWLXHGVDVEVKVKVFVEVGVHPNQYARZMNVIZTVJZXHTOYEUIMNVIYFVLZPY
        GRYEMNVMYHYGYFPYFVNABCDEMNJVOVPVAYEMNTVQVRWAVSVT $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d A a m n u $.  $d B a n u v $.  $d G a m n u v x y z $.
      $d X m n x y z $.
      $( The value of the couple ` <. A , B >. ` through the derived
         operation.  (Contributed by FL, 18-Apr-2010.) $)
      iscst2 $p |- ( ( G e. M /\ A e. ~P X /\ B e. ~P X ) ->
        ( A H B ) = { a | E. u e. A E. v e. B a = ( u G v ) } ) $=
        ( vx vy vz wcel co cv wceq wrex wi vm vn cpw w3a copab2 wa eleq1 anbi1d
        cab opreq1 abbidv eqeq12d imbi12d anbi2d opreq2 rexbidv ab2rexex df-3an
        rexeq oprabbii oprabval2 vtocl2g pm2.43i 3adant1 wb iscst1 opreq eqeq1d
        vex a1i12 com3r syl 3imp mpbird ) EGOZCHUCZOZDVPOZUDCDFPZIQBQAQEPZRZADS
        ZBCSZIUIZRZCDLQZVPOZMQZVPOZNQWAAWHSZBWFSZIUIZRZUDZLMNUEZPZWDRZVQVRWQVOV
        QVRUFZWQUAQZVPOZUBQZVPOZUFZWSXAWOPZWAAXASZBWSSZIUIZRZTVQXBUFZCXAWOPZXEB
        CSZIUIZRZTWRWQTUAUBCDVPVPWSCRZXCXIXHXMXNWTVQXBWSCVPUGUHXNXDXJXGXLWSCXAW
        OUJXNXFXKIXEBWSCUSUKULUMXADRZXIWRXMWQXOXBVRVQXADVPUGUNXOXJWPXLWDXADCWOU
        OXOXKWCIXOXEWBBCWAAXADUSUPUKULUMLMNWSXAVPVPWLXGWOWJBWSSZIUIBAIWSXAVTUAV
        IUBVIUQWFWSRWKXPIWJBWFWSUSUKWHXARZXPXFIXQWJXEBWSWAAWHXAUSUPUKWNWGWIUFWM
        UFLMNWGWIWMURUTVAVBVCVDVOVQVRWEWQVEZVOFWORZVQVRXRTTLMNABGEFHIJKVFVQVRXS
        XRVQVRXSXRTXSVSWPWDCDFWOVGVHVJVKVLVMVN $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d A a u v $.  $d B a u v $.  $d C a u v $.  $d G a u v $.
      $( Property equivalent to the fact of belonging to the value of a pair
         through the derived operation.  (Contributed by FL, 18-Apr-2010.) $)
      iscst3 $p |- ( ( C e. D /\ ( G e. M /\ A e. ~P X /\ B e. ~P X ) ) ->
        ( C e. ( A H B ) <->
           E. u e. A E. v e. B C = ( u G v ) ) ) $=
        ( va wcel cpw w3a co cv wceq wrex wa iscst2 adantl eqeq1 2rexbidv elabg
        cab eleq2d wb adantr bitrd ) EFNZGINCJOZNDUMNPZUAZECDHQZNEMRZBRARGQZSZA
        DTBCTZMUGZNZEURSZADTBCTZUOUPVAEUNUPVASULABCDGHIJMKLUBUCUHULVBVDUIUNUTVD
        MEFUQESUSVCBACDUQEURUDUEUFUJUK $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d A a u x $.  $d B a u v x $.  $d G a u v x $.  $d G a u w x $.
      $d M a u x $.  $d X a u x $.
      $( The value of the couple ` <. A , B >. ` through the derived operation
         ` H ` (expressed with a union).  (Contributed by FL, 31-Dec-2010.) $)
      iscst4 $p |- ( ( G e. M /\ A e. ~P X /\ B e. ~P X )
        -> ( A H B ) = U_ x e. B ( A H { x } ) ) $=
        ( va vu vv vw wcel co cv wceq wrex cab wa cpw ciun iscst2 opreq2 eqeq2d
        w3a csn cbvrexv wex df-rex elsn anbi1i exbii vex ceqsexv 3bitrri rexbii
        wb syl5bb rexbidv rexcom syl6bb abbidv iunab syl6eqr wral simpl1 simpl2
        a1i elelpwi ancoms snelpw 3ad2antl3 syl3anc ralrimiva iuneq2 eqcomd syl
        sylib 3eqtrd ) DFNZBGUAZNZCWBNZUFZBCEOJPZKPZLPZDOZQZLCRZKBRZJSZACWFWGMP
        ZDOZQZMAPZUGZRZKBRZJSZUBZACBWREOZUBZLKBCDEFGJHIUCWEWMWTACRZJSXBWEWLXEJW
        EWLWSACRZKBRXEWEWKXFKBWKWFWGWQDOZQZACRZWEXFWJXHLACWHWQQWIXGWFWHWQWGDUDU
        EUHXIXFURWEXHWSACWSWNWRNZWPTZMUIWNWQQZWPTZMUIXHWPMWRUJXKXMMXJXLWPMWQUKU
        LUMWPXHMWQAUNZXLWOXGWFWNWQWGDUDUEUOUPUQVIUSUTWSKABCVAVBVCWTAJCVDVEWEXCX
        AQZACVFZXBXDQWEXOACWEWQCNZTWAWCWRWBNZXOWAWCWDXQVGWAWCWDXQVHWDWAXQXRWCWD
        XQTWQGNZXRXQWDXSWQCGVJVKWQGXNVLVSVMMKBWRDEFGJHIUCVNVOXPXDXBACXCXAVPVQVR
        VT $.
        $( [31-Dec-2010] $)
    $}
  $}

