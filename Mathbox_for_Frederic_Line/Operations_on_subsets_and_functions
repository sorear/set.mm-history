$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Operations on subsets and functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c cset $.

  $( Extend class notation with an operator that derives an operation on
     subsets of a set from an operation on the elements of this set. $)
  ccst $a class cset $.

  ${
    $d g h x y z a u v $.
    $( Define an operation ` h ` on the subsets derived from an operation ` g `
       on the elements.  Meaningful if ` g ` is a binary internal operation. $)
    df-cst $a |- cset = { <. g , h >. | h = { <. <. x , y >. , z >. |
     ( x e. ~P dom dom g /\ y e. ~P dom dom g
          /\ z = { a | E. u e. x E. v e. y a = ( u g v ) } ) } } $.
  $}

  ${
    iscst1.1 $e |- X = dom dom G $.
    iscst1.2 $e |- H = ( cset ` G ) $.
    ${
      $d G a g h u v x y z $.  $d X g h x y z $.
      $( An operation on the subsets derived from an operation on the
         elements. $)
      iscst1 $p |- ( G e. A -> H = { <. <. x , y >. , z >. |
        ( x e. ~P X /\ y e. ~P X
           /\ z = { a | E. u e. x E. v e. y a = ( u G v ) } ) } ) $=
        ( vg vh wcel ccst cfv cv cpw co wceq wrex cab w3a copab2 cvv elisset
        wa cdm dmexg syl syl5eqel pwexg jca df-3an oprabbii oprabex2g dmeq
        dmeqd pweq ax-mp syl6eqr eleq2d opreq eqeq2d rexbidv abbidv 3anbi123d
        oprabbidv copab cres wrel relopab resid df-cst syl6reqr resopab eqtri
        fvopab4g syl11anc syl5eq ) GFOZHGPQZARZISZOZBRZWEOZCRZJRZERZDRZGTZUAZDW
        GUBZEWDUBZJUCZUAZUDZABCUEZLWBGUFOWTUFOZWCWTUAGFUGWBWEUFOZXBUHZXAWBIUFOZ
        XCWBIGUIZUIZUFKWBXEUFOXFUFOGFUJXEUFUJUKULXDXBXBIUFUMZXGUNUKABCWEWEWQUFU
        FWTWSWFWHUHWRUHABCWFWHWRUOUPUQUKMNGWDMRZUIZUIZSZOZWGXKOZWIWJWKWLXHTZUAZ
        DWGUBZEWDUBZJUCZUAZUDZABCUEZWTUFUFPXHGUAZXTWSABCYBXLWFXMWHXSWRYBXKWEWDY
        BXKXFSZWEYBXJXFUAXKYCUAYBXIXEXHGURUSXJXFUTUKIXFUAWEYCUAKIXFUTVAVBZVCYBX
        KWEWGYDVCYBXRWQWIYBXQWPJYBXPWOEWDYBXOWNDWGYBXNWMWJWKWLXHGVDVEVFVFVGVEVH
        VIPNRYAUAZMNVJZUFVKZXHUFOYEUHMNVJYFVLZPYGUAYEMNVMYHYGYFPYFVNABCDEMNJVOV
        PVAYEMNUFVQVRVSVTWA $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d A a m n u $.  $d B a n u v $.  $d G a m n u v x y z $.
      $d X m n x y z $.
      $( The value of the couple ` <. A , B >. ` through the derived
         operation. $)
      iscst2 $p |- ( ( G e. M /\ A e. ~P X /\ B e. ~P X ) ->
        ( A H B ) = { a | E. u e. A E. v e. B a = ( u G v ) } ) $=
        ( vx vy vz vm vn wcel cpw w3a co cv wceq wrex cab copab2 wa wi eleq1
        anbi1d opreq1 rexeq abbidv eqeq12d imbi12d anbi2d opreq2 rexbidv
        visset ab2rexex df-3an oprabbii oprabval2 vtocl2g pm2.43i 3adant1 wb
        iscst1 opreq eqeq1d a1i12 com3r syl 3imp mpbird ) EGQZCHRZQZDVPQZSCDFTZ
        IUABUAAUAETZUBZADUCZBCUCZIUDZUBZCDLUAZVPQZMUAZVPQZNUAWAAWHUCZBWFUCZIUDZ
        UBZSZLMNUEZTZWDUBZVQVRWQVOVQVRUFZWQOUAZVPQZPUAZVPQZUFZWSXAWOTZWAAXAUCZB
        WSUCZIUDZUBZUGVQXBUFZCXAWOTZXEBCUCZIUDZUBZUGWRWQUGOPCDVPVPWSCUBZXCXIXHX
        MXNWTVQXBWSCVPUHUIXNXDXJXGXLWSCXAWOUJXNXFXKIXEBWSCUKULUMUNXADUBZXIWRXMW
        QXOXBVRVQXADVPUHUOXOXJWPXLWDXADCWOUPXOXKWCIXOXEWBBCWAAXADUKUQULUMUNLMNW
        SXAVPVPWLXGWOWJBWSUCZIUDBAIWSXAVTOURPURUSWFWSUBWKXPIWJBWFWSUKULWHXAUBZX
        PXFIXQWJXEBWSWAAWHXAUKUQULWNWGWIUFWMUFLMNWGWIWMUTVAVBVCVDVEVOVQVRWEWQVF
        ZVOFWOUBZVQVRXRUGUGLMNABGEFHIJKVGVQVRXSXRVQVRXSXRUGXSVSWPWDCDFWOVHVIVJV
        KVLVMVN $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d A a u v $.  $d B a u v $.  $d C a u v $.  $d G a u v $.
      $( Property equivalent to the fact of belonging to the value of a pair
         through the derived operation. $)
      iscst3 $p |- ( ( C e. D /\ ( G e. M /\ A e. ~P X /\ B e. ~P X ) ) ->
        ( C e. ( A H B ) <->
           E. u e. A E. v e. B C = ( u G v ) ) ) $=
        ( va wcel cpw w3a wa co cv wceq wrex cab iscst2 adantl eleq2d wb eqeq1
        2rexbidv elabg adantr bitrd ) EFNZGINCJOZNDUMNPZQZECDHRZNEMSZBSASGRZTZA
        DUABCUAZMUBZNZEURTZADUABCUAZUOUPVAEUNUPVATULABCDGHIJMKLUCUDUEULVBVDUFUN
        UTVDMEFUQETUSVCBACDUQEURUGUHUIUJUK $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d A a u x $.  $d B a u v x $.  $d G a u v x $.  $d G a u w x $.
      $d M a u x $.  $d X a u x $.
      $( The value of the couple ` <. A , B >. ` through the derived operation
         ` H ` (expressed with a union). $)
      iscst4 $p |- ( ( G e. M /\ A e. ~P X /\ B e. ~P X )
        -> ( A H B ) = U_ x e. B ( A H { x } ) ) $=
        ( va vu vv vw wcel cpw w3a co cv wceq wrex cab csn ciun iscst2 opreq2
        eqeq2d cbvrexv wb wa wex df-rex elsn anbi1i exbii visset ceqsexv
        3bitrri rexbii a1i syl5bb rexbidv rexcom syl6bb abbidv iunab syl6eqr
        wral simpl1 simpl2 elelpwi ancoms snelpw sylib 3ad2antl3 syl111anc
        r19.21aiva iuneq2 eqcomd syl 3eqtrd ) DFNZBGOZNZCWBNZPZBCEQJRZKRZLRZDQZ
        SZLCTZKBTZJUAZACWFWGMRZDQZSZMARZUBZTZKBTZJUAZUCZACBWREQZUCZLKBCDEFGJHIU
        DWEWMWTACTZJUAXBWEWLXEJWEWLWSACTZKBTXEWEWKXFKBWKWFWGWQDQZSZACTZWEXFWJXH
        LACWHWQSWIXGWFWHWQWGDUEUFUGXIXFUHWEXHWSACWSWNWRNZWPUIZMUJWNWQSZWPUIZMUJ
        XHWPMWRUKXKXMMXJXLWPMWQULUMUNWPXHMWQAUOZXLWOXGWFWNWQWGDUEUFUPUQURUSUTVA
        WSKABCVBVCVDWTAJCVEVFWEXCXASZACVGZXBXDSWEXOACWEWQCNZUIWAWCWRWBNZXOWAWCW
        DXQVHWAWCWDXQVIWDWAXQXRWCWDXQUIWQGNZXRXQWDXSWQCGVJVKWQGXNVLVMVNMKBWRDEF
        GJHIUDVOVPXPXDXBACXCXAVQVRVSVT $.
        $( [31-Dec-2010] $)
    $}
  $}

