$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( In this section every @d statement has been checked. 10-Feb-2008 $)

  ${
    $d F x y $.  $d G x y $.  $d J x y $.  $d K x y $.  $d L x y $.
    $( The composite of two homeomorphisms is a homeomorphism. $)
    cmphmp $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
     ( ( F e. ( J Homeo K ) /\ G e. ( K Homeo L ) ) ->
     ( G o. F ) e. ( J Homeo L ) ) ) $=
      ( vx vy ctop wcel w3a chomeosm co wa ccom cuni wf1o cv cima wral ccnv wi
      eqid hmeomap 3adant3 3adant1 anim12d imp pm3.22 f1oco 3syl hmeocnb hbra1
      ax-17 hbral hban wal df-ral pm3.35 wceq imaeq2 eleq1d rcla4v syl expimpd
      imaco eqcomi eleq1i biimpi syl6com ex a4s sylbi r19.21ai hmeocna cnvco
      imaeq1i eqtr2i impcom 3jca wb cvv ishomeo 3expia 3adant2 coexg ancoms
      syl5 mpbird ) CHIZDHIZEHIZJZACDKLZIZBDEKLZIZMZBANZCEKLIZWLWQMZWSCOZEOZWRP
      ZWRFQZRZEIZFCSZWRTZXDRZCIZFESZJZWTXCXGXKWTXADOZAPZXMXBBPZMZXOXNMXCWLWQXPW
      LWNXNWPXOWIWJWNXNUAWKACDXAXMXAUBZXMUBZUCUDWJWKWPXOUAWIBDEXMXBXRXBUBZUCUEU
      FUGXNXOUHXAXMXBBAUIUJWTAXDRZDIZFCSZBGQZRZEIZGDSZMZXGWLWQYGWLWNYBWPYFWIWJW
      NYBUAWKFACDUKUDWJWKWPYFUAWIGBDEUKUEUFUGYGXFFCYBYFFYAFCULYEFGDYCDIFUMYEFUM
      UNUOYBYFXDCIZXFUAZYBYHYAUAZFUPYFYIUAZYAFCUQYJYKFYJYFYIYHYJYFMBXTRZEIZXFYH
      YJYFYMYHYJMYAYFYMUAYHYAURYEYMGXTDYCXTUSYDYLEYCXTBUTVAVBVCVDYMXFYLXEEXEYLB
      AXDVEVFVGVHVIVJVKVLUGVMVCWTATZYCRZCIZGDSZBTZXDRZDIZFESZMZXKWLWQUUBWLWNYQW
      PUUAWIWJWNYQUAWKGACDVNUDWJWKWPUUAUAWIFBDEVNUEUFUGUUBXJFEYQUUAFYQFUMYTFEUL
      UOUUAYQXDEIZXJUAZUUAUUCYTUAZFUPYQUUDUAZYTFEUQUUEUUFFUUEYQUUDUUCUUEYQMYNYS
      RZCIZXJUUCUUEYQUUHUUCUUEMYTYQUUHUAUUCYTURYPUUHGYSDYCYSUSYOUUGCYCYSYNUTVAV
      BVCVDUUHXJUUGXICXIYNYRNZXDRUUGXHUUIXDBAVOVPYNYRXDVEVQVGVHVIVJVKVLVRVMVCVS
      WLWQWSXLVTZWLWRWAIZUUJWQWIWKUUKUUJUAWJWIWKUUKUUJFWAWRCEXAXBXQXSWBWCWDWPWN
      UUKBAWOWMWEWFWGUGWHVJ $.
      $( [11-Mar-2007] $) $( [9-Mar-2007] $)
  $}

  ${
    $d J x $.  $d X x $.
    idhme.1 $e |- X = U. J $.
    $( The identity function is a homeomorphism. $)
    idhme $p |- ( J e. Top -> ( _I |` X ) e. ( J Homeo J ) ) $=
      ( vx ctop wcel cid cres cvv chomeosm co cuni uniexg syl5eqel resiexg syl
      w3a wf1o cv cima wral ccnv f1oi a1i wss wceq elssuni id syl6ssr resiima
      3syl eqeltrd rgen cnvresid imaeq1d eqtrd 3jca ishomeo mpbird 3anidm12
      mpdan ) AEFZGBHZIFZVCAAJKFZVBBIFVDVBALZIBAEMCNBIOPVBVDVEVBVBVDQZVEBBVCRZV
      CDSZTZAFZDAUAZVCUBZVITZAFZDAUAZQVGVHVLVPVHVGBUCUDVLVGVKDAVIAFZVJVIAVQVIVF
      UEZVIBUEZVJVIUFVIAUGZVRVIVFBVRUHCUIZBVIUJZUKVQUHZULUMUDVPVGVODAVQVNVIAVQV
      RVNVIUFZVTVRVSWDWAVSVNVJVIVSVMVCVIVMVCUFVSBUNUDUOWBUPPPWCULUMUDUQDIVCAABB
      CCURUSUTVA $.
      $( [20-Feb-2007] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The converse of a homeomorphism is a homeomorphism. $)
    cnvhmphb $p |- ( ( J e. Top /\ K e. Top /\ Rel F ) ->
      ( `' F e. ( J Homeo K ) -> F e. ( K Homeo J ) ) ) $=
      ( vx ccnv chomeosm co wcel ctop wrel w3a cvv wi cnvexg wa wceq dfrel2
      biimpi eleq1d cuni wf1o cv cima wral eqid hmeomap f1ocnvb biimprd syl9
      imp31 hmeocna imaeq1d ralbidv biimpd hmeocnb adantr imp 3jca exp31
      ancoms 3adant3 wb ishomeo sylibrd 3exp1 impcom com3l syl6bi pm2.43a
      com3r 3impia mpcom com12 ) AEZBCFGZHZBIHZCIHZAJZKZACBFGHZVNEZLHZVPVTWAMVN
      VONVTWCVPWAVQVRVSWCVPWAMZMVSWCVQVROZWDWCVSWEWDMZVSWCALHZVSWFMVSWBALVSWBAP
      AQRZSWEWGVSWDVRVQWGVSWDMMVRVQWGVSWDVRVQWGKZVSOVPCTZBTZAUAZADUBZUCZBHZDCUD
      ZVNWMUCCHDBUDZKZWAWIVSVPWRMZVRVQVSWSMZWGVQVRWTWEVSVPWRWEVSOZVPOWLWPWQWEVS
      VPWLWEVPWKWJVNUAZVSWLVNBCWKWJWKUEZWJUEZUFVSWLXBWJWKAUGUHUIUJWEVSVPWPWEVPW
      BWMUCZBHZDCUDZVSWPDVNBCUKVSXGWPVSXFWODCVSXEWNBVSWBAWMWHULSUMUNUIUJXAVPWQW
      EVPWQMVSDVNBCUOUPUQURUSUTVAUQWIWAWRVBVSDLACBWJWKXDXCVCUPVDVEVFVGVHVIVJVKV
      GVLVM $.
      $( [11-Mar-2007] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F j $.  $d J j $.  $d K j $.
    $( The converse of a homeomorphism is a homeomorphism. $)
    cnvhmph $p |- ( ( ( J e. Top /\ K e. Top /\ F e. A ) /\ Rel F ) ->
      ( F e. ( J Homeo K ) <-> `' F e. ( K Homeo J ) ) ) $=
      ( vj ctop wcel w3a wrel wa cuni ccnv wf1o cv cima wral chomeosm co wb
      f1ocnvb bicomd adantl wceq dfrel2 biimpi imaeq1d eleq1d ralbidv 3anbi12d
      3ancomb syl5bb cvv id cnvexg 3anim123i 3com12 adantr eqid ishomeo syl
      3bitr4rd ) CFGZDFGZBAGZHZBIZJZDKZCKZBLZMZVJENZOCGEDPZVJLZVLOZDGZECPZHZVIV
      HBMZBVLOZDGZECPZVMHZVJDCQRGZBCDQRGZVGVKVQVMHWCVRVGVKVSVQWBVMVFVKVSSVEVFVS
      VKVIVHBTUAUBVFVQWBSVEVFVPWAECVFVOVTDVFVNBVLVFVNBUCBUDUEUFUGUHUBUIVKVMVQUJ
      UKVGVCVBVJULGZHZWDVRSVEWGVFVCVBVDWGVCVCVBVBVDWFVCUMVBUMBAUNUOUPUQEULVJDCV
      HVIVHURZVIURZUSUTVEWEWCSVFEABCDVIVHWIWHUSUQVA $.
      $( [5-Mar-2007] $) $( [15-Feb-2007] $)
  $}

  ${
    $d J f g $.  $d J f x $.  $d K f g $.  $d K f x $.
    $( "Is homeomorph to" is symmetric. $)
    hmphsyma $p |- ( ( J e. Top /\ K e. Top ) ->
        ( J ~= K -> K ~= J ) ) $=
      ( vf vg vx ctop wcel wa cv chomeosm co wex chomeo wbr cvv wi visset w3a
      cuni wf1o cima wral ccnv eqid ishomeo cnvexg 3ad2ant3 adantr wrel f1orel
      3ad2ant1 adantl f1ocnv a1i wceq dfrel2 biimpi eqcomd imaeq1d eleq1d
      biimpd ad2antrr ralimdva idd 3anim123d expimpd mpcom 3ancomb sylib wb
      syl3an3 3com12 mpbird eleq1 cla4egv sylc ex sylbid mp3an3 19.23adv hmph
      ancoms 3imtr4d ) AFGZBFGZHZCIZABJKGZCLDIZBAJKZGZDLZABMNBAMNZWFWHWLCWDWEWG
      OGZWHWLPCQWDWEWNRZWHASZBSZWGTZWGEIZUAZBGZEAUBZWGUCZWSUAAGEBUBZRZWLEOWGABW
      PWQWPUDZWQUDZUEWOXEWLWOXEHZXCOGZXCWJGZWLWOXIXEWNWDXIWEWGOUFZUGUHXHXJWQWPX
      CTZXDXCUCZWSUAZBGZEAUBZRZXHXLXPXDRZXQWGUIZXHXRXEXSWOWRXBXSXDWPWQWGUJUKULX
      SWOXEXRXSWOHZWRXLXBXPXDXDWRXLPXTWPWQWGUMUNXTXAXOEAXSXAXOPWOWSAGXSXAXOXSWT
      XNBXSWGXMWSXSXMWGXSXMWGUOWGUPUQURUSUTVAVBVCXTXDVDVEVFVGXLXPXDVHVIWOXJXQVJ
      ZXEWEWDWNYAWEWDXIYAWNEOXCBAWQWPXGXFUEXKVKVLUHVMWKXJDXCOWIXCWJVNVOVPVQVRVS
      VTCABWAWEWDWMWLVJDBAWAWBWC $.
      $( [11-Mar-2007] $) $( [8-Mar-2007] $)
  $}


  ${
    $( "Is homeomorph to" is symmetric. $)
    hmphsym $p |- ( ( J e. Top /\ K e. Top ) ->
        ( J ~= K <-> K ~= J ) ) $=
      ( ctop wcel wa chomeo wbr hmphsyma wi ancoms impbid ) ACDZBCDZEABFGZBAFGZ
      ABHMLONIBAHJK $.
      $( [11-Mar-2007] $) $( [22-Feb-2007] $)
  $}

  ${
    $d J f $.  $d J x $.
    $( "Is homeomorph to" is reflexive. $)
    hmphre $p |- ( J e. Top -> J ~= J ) $=
      ( vf vx ctop wcel chomeo wbr wa cv chomeosm co wex cuni cvv wi uniexg
      cid cres resiexg w3a wf1o cima wral ccnv f1oi a1i wss elssuni resiima
      eleq1d biimprcd mpd rgen wceq adantl simpl eqeltrd cnvresid imaeq1i
      syl5eqel mpdan 3jca eqid ishomeo mpbird 3exp pm2.43i com12 eleq1 imbi2d
      cla4egv 19.37v sylib syl imp hmph anidms ) ADEZAAFGZVRVRHVSBIZAAJKZEZBLZV
      RVRWCVRAMZNEZVRWCOZADPWEQWDRZNEZWFWDNSWHVRWBOZBLZWFWHVRWGWAEZOZWJVRWHWKVR
      WHWKOVRVRWHWKVRVRWHTZWKWDWDWGUAZWGCIZUBZAEZCAUCZWGUDZWOUBZAEZCAUCZTWMWNWR
      XBWNWMWDUEUFWRWMWQCAWOAEZWOWDUGZWQWOAUHZXDWQXCXDWPWOAWDWOUIZUJUKULUMUFXBW
      MXACAXCXDXAXEXCXDHZWPAWTXGWPWOAXDWPWOUNXCXFUOXCXDUPUQWSWGWOWDURUSUTVAUMUF
      VBCNWGAAWDWDWDVCZXHVDVEVFVGVHWIWLBWGNVTWGUNWBWKVRVTWGWAVIVJVKULVRWBBVLVMV
      NVNVOBAAVPVEVQ $.
      $( [5-Mar-2007] $) $( [25-Feb-2007] $)
  $}

  ${
    $d J f g h $.  $d K f g $.  $d L f g h $.
    $( "Is homeomorph to" is transitive. $)
    hmphtr $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( J ~= K /\ K ~= L ) -> J ~= L ) ) $=
      ( vf vg vh ctop wcel w3a cv chomeosm co wex chomeo wbr wa wi ccom cvv
      coexg adantl cmphmp ancomsd imp eleq1 cla4egv sylc exp32 19.23adv com23
      imp3a wb hmph 3adant2 sylibrd biimpd 3adant3 3adant1 syl2and ) AGHZBGHZCG
      HZIZDJZABKLZHZDMZEJZBCKLZHZEMZACNOZABNOZBCNOZVCVGVKPFJZACKLZHZFMZVLVCVGVK
      VRVCVFVKVRQDVCVKVFVRVCVJVFVRQEVCVJVFVRVCVJVFPZPVHVDRZSHZVTVPHZVRVSWAVCVHV
      DVIVETUAVCVSWBVCVFVJWBVDVHABCUBUCUDVQWBFVTSVOVTVPUEUFUGUHUIUJUIUKUTVBVLVR
      ULVAFACUMUNUOUTVAVMVGQVBUTVAPVMVGDABUMUPUQVAVBVNVKQUTVAVBPVNVKEBCUMUPURUS
      $.
      $( [24-Mar-2007] $) $( [9-Mar-2007] $)
  $}

  ${
    $d x y z $.
    $( ` ~= ` is a relation whose domain is included in ` Top ` . $)
    dmhmph $p |- dom ~= C_ Top $=
      ( vx vy vz chomeo cdm ctop cxp wss cv wcel wa chomeosm co wex copab w3a
      df-hmph df-3an opabbii eqtri opabssxp eqsstri dmss ax-mp dmxpid sseqtri
      ) DEZFFGZEZFDUHHUGUIHDAIZFJZBIZFJZKCIUJULLMJCNZKZABOZUHDUKUMUNPZABOUPABCQ
      UQUOABUKUMUNRSTUNABFFUAUBDUHUCUDFUEUF $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)

    $( ` ~= ` is a relation whose range is included in ` Top ` . $)
    rnhmph $p |- ran ~= C_ Top $=
      ( vx vy vz chomeo crn ctop cxp wss cv wcel wa chomeosm co wex copab w3a
      df-hmph df-3an opabbii eqtri opabssxp eqsstri rnss ax-mp rnxpss sstri )
      DEZFFGZEZFDUHHUGUIHDAIZFJZBIZFJZKCIUJULLMJCNZKZABOZUHDUKUMUNPZABOUPABCQUQ
      UOABUKUMUNRSTUNABFFUAUBDUHUCUDFFUEUF $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)
  $}

  ${
    dmhmpha.1 $e |- A e. _V $.
    $( The relation "being homeomorph to" implies the operands are
       topologies. $)
    dmhmpha $p |- ( A ~= B -> A e. Top ) $=
      ( chomeo wbr cdm wcel ctop breldm dmhmph sseli syl ) ABDEADFZGAHGABDCIMHA
      JKL $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)
  $}

  ${
    rnhmpha.1 $e |- A e. _V $.
    rnhmpha.2 $e |- B e. _V $.
    $( The relation "being homeomorph to" implies the operands are
       topologies. $)
    rnhmpha $p |- ( A ~= B -> B e. Top ) $=
      ( chomeo wbr crn wcel ctop brelrn rnhmph sseli syl ) ABEFBEGZHBIHABECDJNI
      BKLM $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)
  $}


  ${
    $d x y z $.
    $( "Is homeomorph to" is an equivalence relation. $)
    hmpher $p |- Er ~= $=
      ( vx vy vz chomeo cv ctop wcel wa wbr visset dmhmpha rnhmpha jca
      hmphsyma mpcom w3a adantr adantl 3jca hmphtr ster ) ABCDAEZFGZBEZFGZHUBUD
      DIZUDUBDIUFUCUEUBUDAJZKZUBUDUGBJZLMUBUDNOUCUECEZFGZPUFUDUJDIZHZUBUJDIUMUC
      UEUKUFUCULUHQULUEUFUDUJUIKRULUKUFUDUJUICJLRSUBUDUJTOUA $.
      $( [24-Mar-2007] $) $( [22-Mar-2007] $)
  $}

  ${
    hmphsymv.1 $e |- A e. _V $.
    hmphsymv.2 $e |- B e. _V $.
    $( A more general version of ~ hmphsym .  Certainly not a useful proof
       (since it's a simple consequence of ~ hmpher ) but it shows that the
       conditions of ~ hmphsym could (and should) be weakened. $)
    hmphsymv $p |- ( A ~= B <-> B ~= A ) $=
      ( chomeo hmpher ersymb ) ABECDFG $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)

  $}

  ${
    $d J a b c j x y z $.  $d f t u v w $.  $d v w x y z $.
    $( Homeomorphisms on a topology ` J ` is a group for composition.  This
       means from Felix Klein's point of view that a set equipped with a
       topology is a geometry, namely the so-called rubber sheet geometry. $)
    hmeogrp $p |- ( J e. Top -> { <. <. x , y >. , z >. | ( x e. ( J Homeo J )
    /\ y e. ( J Homeo J ) /\ z = ( x o. y ) ) } e. Grp ) $=
      ( va vb vc vj vv vw vt vf vu ctop wcel cv chomeosm co ccom wceq w3a
      copab2 cgr cxp wf wral wrex wa wi cmphmp ancomsd 3exp pm2.43d pm2.43i
      r19.21aivv df-3an oprabbii foprab2 sylib coass cvv simpr1 simpr2 coexg
      3adant3 adantl coeq1 coeq2 oprabval2g opreq1d syl111anc com12 ancoms
      impcom simpr3 simp3 syl11anc eqtrd 3adant1 opreq2d 3eqtr4a 3exp2 imp32
      r19.21aiv ex cid cuni cres eqid idhme adantr simpr exp3a mpid imp wf1o
      hmeomap anidms f1of fcoi2 3syl ccnv cnvhmpha expdimp mpd f1ococnv1 syl
      opreq1 eqeq1d rcla4ev jca r19.21aiva eqeq2 rexbidv anbi12d ralbidv 3jca
      wb oprex pm3.2i a1i oprabex2g wex cab crn eleq1a syl6 com3l expcom 3imp
      19.23aivv fcoi1 eqcomd eqeq2d anbi2d syl12anc 3ancoma 3anass bitri exbii
      df-rex bitr4i sylibr eleq1 bicomd eqeq1 3anbi3d exbidv imbi12d mpbiri
      a4imev impbid abbi1dv wn simpl con3i c0 cdm cima wfun wf1 dff1o5 df-f1
      anbi1i anass 3bitri abbii visset uniex fabex eqeltri df-homeo dmoprab2
      ndmoprg mp3an1 eleq2 noel pm2.21i syl6bi simp1 syl5 19.23advv impbid1
      eqtr4d wrel reldmoprab dmeqi releqi mpbir oprprc1 a1d pm2.61i rnoprab
      eqtr4i isgrp mpbird ) DNOZAPZDDQRZOZBPZUWSOZCPZUWRUXASZTZUAZABCUBZUCOZUWS
      UWSUDUWSUXGUEZEPZFPZUXGRZGPZUXGRZUXJUXKUXMUXGRZUXGRZTZGUWSUFZFUWSUFEUWSUF
      ZHPZUXJUXGRZUXJTZUXKUXJUXGRZUXTTZFUWSUGZUHZEUWSUFZHUWSUGZUAZUWQUXIUXSUYHU
      WQUXDUWSOZBUWSUFAUWSUFUXIUWQUYJABUWSUWSUWQUWTUXBUHZUYJUIZUWQUWQUYLUWQUWQU
      WQUYLUWQUWQUWQUAZUXBUWTUYJUXAUWRDDDUJZUKULUMUNUOABCUWSUWSUXDUWSUXGUXFUYKU
      XEUHABCUWTUXBUXEUPUQZURUSUWQUXREFUWSUWSUWQUXJUWSOZUXKUWSOZUHZUXRUWQUYRUHU
      XQGUWSUWQUYPUYQUXMUWSOZUXQUIUWQUYPUYQUYSUXQUWQUYPUYQUYSUAZUHZUXJUXKSZUXMS
      ZUXJUXKUXMSZSZUXNUXPUXJUXKUXMUTVUAUXNVUBUXMUXGRZVUCVUAUYPUYQVUBVAOZUXNVUF
      TUWQUYPUYQUYSVBZUWQUYPUYQUYSVCZUYTVUGUWQUYPUYQVUGUYSUXJUXKUWSUWSVDVEZVFUY
      PUYQVUGUAUXLVUBUXMUXGABCUXJUXKUWSUWSUXDVUBUXGUXJUXASZVAUWRUXJUXAVGZUXAUXK
      UXJVHUYOVIVJVKVUAVUBUWSOZUYSVUCVAOZVUFVUCTUYTUWQVUMUYPUYQUWQVUMUIZUYSUYQU
      YPVUOUWQUYQUYPUHZVUMUWQVUPVUMUIZUWQUWQVUQUWQUWQUWQVUQUXKUXJDDDUJULUMUNVLV
      MVEVNUWQUYPUYQUYSVOZUYTVUNUWQUYTVUGUYSVUNVUJUYPUYQUYSVPVUBUXMVAUWSVDVQVFA
      BCVUBUXMUWSUWSUXDVUCUXGVUBUXASVAUWRVUBUXAVGUXAUXMVUBVHUYOVIVKVRVUAUXPUXJV
      UDUXGRZVUEVUAUXOVUDUXJUXGVUAUYQUYSVUDUWSOZUXOVUDTVUIVURUYTUWQVUTUYQUYSUWQ
      VUTUIZUYPUYSUYQVVAUWQUYSUYQUHZVUTUWQVVBVUTUIZUWQUWQVVCUWQUWQUWQVVCUXMUXKD
      DDUJULUMUNVLVMVSVNZABCUXKUXMUWSUWSUXDVUDUXGUXKUXASUWSUWRUXKUXAVGUXAUXMUXK
      VHUYOVIVKVTVUAUYPVUTVUEVAOZVUSVUETVUHVVDVUAUYPVUDVAOZVVEVUHUYTVVFUWQUYQUY
      SVVFUYPUXKUXMUWSUWSVDVSVFUXJVUDUWSVAVDVQABCUXJVUDUWSUWSUXDVUEUXGVUKVAVULU
      XAVUDUXJVHUYOVIVKVRWAWBWCWDWEUOUWQWFDWGZWHZUWSOZVVHUXJUXGRZUXJTZUYCVVHTZF
      UWSUGZUHZEUWSUFZUYHDVVGVVGWIZWJZUWQVVNEUWSUWQUYPUHZVVKVVMVVRVVJVVHUXJSZUX
      JVVRVVIUYPVVSUWSOZVVJVVSTUWQVVIUYPVVQWKUWQUYPWLZUWQUYPVVTUWQUYPVVIVVTVVQU
      WQUYPVVIVVTUIUIZUWQUWQVWBUWQUWQUWQVWBUYMUYPVVIVVTUXJVVHDDDUJWMULUMUNWNWOA
      BCVVHUXJUWSUWSUXDVVSUXGVVHUXASUWSUWRVVHUXAVGUXAUXJVVHVHUYOVIVKVVRVVGVVGUX
      JWPZVVGVVGUXJUEVVSUXJTUWQUYPVWCUWQUYPVWCUIUXJDDVVGVVGVVPVVPWQWRWOZVVGVVGU
      XJWSVVGVVGUXJWTXAVRVVRUXJXBZUWSOZVWEUXJUXGRZVVHTZVVMUWQUYPVWFUWQUYPVWFUIU
      XJDDXCWRWOZVVRVWGVWEUXJSZVVHVVRVWFUYPVWJUWSOZVWGVWJTVWIVWAVVRVWFVWKVWIUWQ
      UYPVWFVWKUWQUYPVWFUHVWKUIZUWQUWQVWLUWQUWQUWQVWLUXJVWEDDDUJULUMUNXDXEABCVW
      EUXJUWSUWSUXDVWJUXGVWEUXASUWSUWRVWEUXAVGUXAUXJVWEVHUYOVIVKVVRVWCVWJVVHTVW
      DVVGVVGUXJXFXGVRVVLVWHFVWEUWSUXKVWETUYCVWGVVHUXKVWEUXJUXGXHXIXJVQXKXLUYGV
      VOHVVHUWSUXTVVHTZUYFVVNEUWSVWMUYBVVKUYEVVMVWMUYAVVJUXJUXTVVHUXJUXGXHXIVWM
      UYDVVLFUWSUXTVVHUYCXMXNXOXPXJVQXQUWQUWSVAOZVWNUHZUXGVAOUXHUYIXRVWOUWQVWNV
      WNDDQXSZVWPXTYAABCUWSUWSUXDVAVAUXGUYOYBEFGHVAUXGUWSUWSUXFBYCZAYCZCYDZUXGY
      EUWQUWSVWSTZUWQVWSUWSUWQVWRCUWSUWQVWRUXCUWSOZVWRUWQVXAUXFUWQVXAUIZABUWTUX
      BUXEVXBUXBUWTUXEVXBUIUWQUXBUWTUHZUXEVXAUWQVXCUXEVXAUIZUIZUWQUWQVXEUWQUWQU
      WQVXEUYMVXCUYJVXDUYNUXDUWSUXCYFYGULUMUNYHYIYJYKVLUWQVXAVWRUWQVXAUHZVWQACU
      WRUXCTZVXFVWQUIUWQUWTUHZUWTUXBUWRUXDTZUAZBYCZUIVXHUWTVXIUHZBUWSUGZVXKVXHV
      VIUWTUWRUWRVVHSZTZVXMUWQVVIUWTVVQWKUWQUWTWLUWQUWTVXOUWQUWTVXOUIUWQUWQUHZU
      WTVVGVVGUWRWPZVXOUWRDDVVGVVGVVPVVPWQVXQVXNUWRVXQVVGVVGUWRUEVXNUWRTVVGVVGU
      WRWSVVGVVGUWRYLXGYMYGWRWOVXLUWTVXOUHBVVHUWSUXAVVHTZVXIVXOUWTVXRUXDVXNUWRU
      XAVVHUWRVHYNYOXJYPVXKUXBVXLUHZBYCVXMVXJVXSBVXJUXBUWTVXIUAVXSUWTUXBVXIYQUX
      BUWTVXIYRYSYTVXLBUWSUUAUUBUUCVXGVXFVXHVWQVXKVXGVXAUWTUWQVXGUWTVXAUWRUXCUW
      SUUDUUEYOVXGUXFVXJBVXGUXEVXIUWTUXBVXGVXIUXEUWRUXCUXDUUFUUEUUGUUHUUIUUJUUK
      WEUULUUMYMUWQUUNVXPUUNZVWTVXPUWQUWQUWQUUOUUPDVAOZVXTVWTUIVYAVXTVWTVYAVXTU
      HZUWSUUQVWSQUURZNNUDTVYAVXTUWSUUQTZABCNNUWRWGZUXAWGZIPZWPZVYGJPZUUSUXAOJU
      WRUFZVYGXBZVYIUUSUWROJUXAUFZUAZIYDZQVYNVYEVYFVYGUEZVYKUUTZVYGYEVYFTZUHZUH
      ZVYJVYLUHZUHZIYDZVAVYMWUAIVYMVYHVYTUHWUAVYHVYJVYLYRVYHVYSVYTVYHVYEVYFVYGU
      VAZVYQUHVYOVYPUHZVYQUHVYSVYEVYFVYGUVBWUCWUDVYQVYEVYFVYGUVCUVDVYOVYPVYQUVE
      UVFUVDYSUVGVYRVYTUHZIVYEVYFWUBUWRAUVHUVIUXABUVHUVIWUAVYOWUEUHIVYOVYRVYTUV
      EUVGUVJUVKJCIABUVLUVMDDVANNQUVNUVOZVYBVWRCUUQVYBVWRUXCUUQOZVYBVYDVWRWUGUI
      ZWUFVYDUXFWUGABVYDUWTWUGUXFVYDUWTUWRUUQOZWUGUWSUUQUWRUVPWUIWUGUWRUVQUVRUV
      SUWTUXBUXEUVTUWAUWBZXGWUGVWRUXCUVQUVRZUWCUUMUWDWEVYAUUNZVWTVXTWULUWSUUQVW
      SDDQVYCUWEKPZNOLPZNOUHVYGWUMWGWUNWGMPZWPWUOVYIUUSWUNOJWUMUFWUOXBVYIUUSWUM
      OJWUNUFUAMYDTUHZKLIUBZUURZUWEWUPKLIUWFVYCWURQWUQJIMKLUVLUWGUWHUWIUWJZWULV
      WRCUUQWULVWRWUGWULVYDWUHWUSWUJXGWUKUWCUUMUWDUWKUWLXGUWLUXFABCUWMUWNUWOXAU
      WP $.
      $( [4-Feb-2008] $) $( [3-Feb-2008] $)
  $}

  ${
    $d J a b f g $.  $d J a b f m n x $.  $d K a b f g $.  $d K a b f m n x $.
    $( Homeomorphisms preserve the cardinality of the topologies. $)
    homcard $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K ->
    J ~~ K ) ) $=
      ( vf vx vg va vb vm vn ctop wcel wa chomeo wbr cv chomeosm co wex cen
      hmph wi w3a cuni wf1o cima wral ccnv eqid ishomeo wceq copab cvv
      opabex2g 3ad2ant1 adantr wf1 crn wf cfv wfn wss visset imaexg ax-mp a1i
      r19.21aiva fnopab2g sylib wrex cab wal eleq1 imbi2d imaeq2 eleq1d
      rcla4va expcom 3ad2ant2 adantl com12 syl5cbir r19.23aiv 19.21aiv abss
      sylibr rnopab2 syl5ss jca df-f f1of1 fvopab4g ex im2anan9 eqtr syl
      eqcoms imp3a eltopss anim12d oooeqim2 biimpd 3expib syl6 com24 com14
      exp3a syld anim12i syl5com mp2an 3adant3 impcom r19.21aivv dff13 wb
      hbra1 ax-17 hban rcla4v eleq1a a1d com3l imp 19.21ai ra4 elssuni eqeq1
      eqcom syl5bb rcla4ev f2imacnv imim2d pm2.43a 3adant2 albiim rexbid
      bibi12d cbval abeq1 syl5eq dff1o5 f1oeq1 cla4egv sylc breng mpbird
      sylbid 3expia pm2.43i 19.23aiv ) AJKZBJKZLZABMNCOZABPQZKZCRZABSNZCABTUUQU
      UMUURUUPUUMUURUAZCUUPUUSUUMUUPUUPUURUUKUULUUPUUPUURUAUUKUULUUPUBZUUPAUCZB
      UCZUUNUDZUUNDOZUEZBKZDAUFZUUNUGUVDUEZAKZDBUFZUBZUURDUUOUUNABUVAUVBUVAUHZU
      VBUHUIUUTUVKUURUUTUVKLZUURABEOZUDZERZUVMFOZAKZGOZUUNUVQUEZUJZLFGUKZULKZAB
      UWBUDZUVPUUTUWCUVKUUKUULUWCUUPFGAUVTJUMUNUOUVMABUWBUPZUWBUQZBUJZLUWDUVMUW
      EUWGUVMABUWBURZHOZUWBUSZIOZUWBUSZUJZUWIUWKUJZUAZIAUFHAUFZLUWEUVMUWHUWPUVM
      UWBAUTZUWFBVAZLUWHUVMUWQUWRUVMUVTULKZFAUFUWQUVMUWSFAUWSUVMUVRLUUNULKZUWSC
      VBZUUNUVQULVCVDVEVFFGAUVTUWBUWBUHZVGVHUVMUWAFAVIZGVJZBUWFUVMUXCUVSBKZUAZG
      VKUXDBVAUVMUXFGUXCUVMUXEUWAUVMUXEUAZFAUWAUXGUVMUVTBKZUAUVRUWAUXEUXHUVMUVS
      UVTBVLVMUVMUVRUXHUVKUVRUXHUAZUUTUVGUVCUXIUVJUVRUVGUXHUVFUXHDUVQAUVDUVQUJU
      VEUVTBUVDUVQUUNVNVOZVPVQVRVSVTWAWBVTWCUXCGBWDWEFGAUVTWFZWGWHABUWBWIWEUVMU
      WOHIAAUVKUUTUWIAKZUWKAKZLZUWOUAZUVCUVGUUTUXOUAUVJUUTUVCUXOUUKUULUVCUXOUAU
      UPUVCUUMUXOUVCUVAUVBUUNUPZUUMUXOUAZUVAUVBUUNWJUWTUWTUXPUXQUAUXAUXAUXPUUNU
      WIUEZULKZUUNUWKUEZULKZLZUXQUWTUWTLUXNUYBUUMUXPUWOUXNUYBUWJUXRUJZUWLUXTUJZ
      LZUUMUXPUWOUAZUAUXLUXSUYCUXMUYAUYDUXLUXSUYCFGUWIUVTUXRAULUWBUVQUWIUUNVNUX
      BWKWLUXMUYAUYDFGUWKUVTUXTAULUWBUVQUWKUUNVNUXBWKWLWMUXNUYEUUMUYFUWMUYEUUML
      UXPUXNUWNUWMUYEUUMUXPUXNUWNUAUAZUWMUYEUXRUXTUJZUUMUYGUAUWMUYCUYDUYHUYCUWM
      UYDUYHUAZUWMUYIUAUXRUWJUXRUWJUJZUWMUYIUYJUWMLUXRUWLUJZUYIUXRUWJUWLWNUYKUY
      DUYHUXRUWLUXTWNWLWOWLWPVTWQUUMUYHUYGUUMUXNUXPUYHUWNUUMUXNUWIUVAVAZUWKUVAV
      AZLZUXPUYHUWNUAZUAUUKUXNUYNUAUULUUKUXLUYLUXMUYMUUKUXLUYLUWIAUVAUVLWRWLUUK
      UXMUYMUWKAUVAUVLWRWLWSUOUXPUYNUYOUXPUYLUYMUYOUXPUYLUYMUBUYHUWNUVAUVBUUNUW
      IUWKWTXAXBVTXCXDVTXCWQXEXFXGXEUWTUXSUWTUYAUUNUWIULVCUUNUWKULVCXHXIXJWOVTX
      KVTUNXLXMWHHIABUWBXNWEUVMUXDBUWFUVMUXCUXEXOZGVKZUXDBUJUVMUVDUVTUJZFAVIZUV
      DBKZXOZDVKZUYQUVMUYSUYTUAZDVKZUYTUYSUAZDVKZLVUBUVMVUDVUFUVKUUTVUDUVGUVCUU
      TVUDUAUVJUVGUUTVUDUVGUUTLZVUCDUVGUUTDUVFDAXPUUTDXQXRUYSVUGUYTUYRVUGUYTUAF
      AVUGUVRUYRUYTUVGUUTUVRUYRUYTUAZUAUVRUVGUUTVUHUVRUVGUXHUUTVUHUAUVFUXHDUVQA
      UXJXSUXHVUHUUTUVTBUVDXTYAXCYBYCYBWBVTYDWLVRXLUVKVUFUUTUVCUVJVUFUVGUVCUVJL
      VUEDUVCUVJDUVCDXQUVIDBXPXRUVJUVCVUEUVJUYTUVIUAZUVCVUEUAUVIDBYEUYTVUIUVCUY
      SVUIUYTUVCUYSUAZUYTUVIVUJUYTUYTUVDUVBVAZUVIVUJUAUVDBYFUVCVUKUVIUYSUVCVUKU
      VIUYSUAUVIUUNUVHUEZUVDUJZUYSUVCVUKLUVIVUMUYSUYRVUMFUVHAUVQUVHUJUVTVULUJZU
      YRVUMXOUVQUVHUUNVNVUNUVTUVDUJVUMUYRUVTVULUVDYGUVDUVTYHYIWOYJWLUVAUVBUVDUU
      NYKXIWLYBWOYLYMYBWOXLYDYNVSWHUYSUYTDYOWEVUAUYPDGVUAGXQUYPDXQUVDUVSUJZUYSU
      XCUYTUXEVUOUYRUWAFAVUOFXQUVDUVSUVTYGYPUVDUVSBVLYQYRVHUXCGBYSWEUXKYTWHABUW
      BUUAWEUVOUWDEUWBULABUVNUWBUUBUUCUUDUUTUURUVPXOZUVKUULUUKVUPUUPABJEUUEVRUO
      UUFWLUUGUUHYBUUIUUJVTUUG $.
      $( [1-Jun-2008] $)
  $}

  ${
    $d J f $.  $d K f $.  $d X f $.  $d Y f $.
    homcardus.1 $e |- X = U. J $.
    homcardus.2 $e |- Y = U. K $.
    $( Homeomorphisms preserve the cardinality of the underlying sets. $)
    homcardus $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K -> X ~~ Y ) ) $=
      ( vf ctop wcel wa cv chomeosm co wex wf1o chomeo wbr cen hmeomap eximdv
      hmph cvv wb cuni uniexg syl5eqel adantl breng syl 3imtr4d ) AHIZBHIZJZGKZ
      ABLMIZGNCDUNOZGNZABPQCDRQZUMUOUPGUNABCDEFSTGABUAUMDUBIZURUQUCULUSUKULBUDU
      BDBHUEFUFUGCDUBGUHUIUJ $.
      $( [1-Sep-2008] $) $( [17-Aug-2008] $)
  $}

  ${
    $d A f x $.  $d B f x $.
    eqindhome.1 $e |- A e. C $.
    eqindhome.2 $e |- B e. D $.
    $( Equinumerous sets equipped with their indiscrete topologies are
       homeomorph (which means in particular that a segment is homeomorph to a
       circle contrary to what Wikipedia claims). $)
    eqindhome $p |- ( A ~~ B -> { (/) , A } ~= { (/) , B } ) $=
      ( vf vx cv wf1o wex c0 cpr chomeosm co wcel cen wbr chomeo cima wral
      ccnv w3a id wceq wo imaeq2 eqeq1d ima0 a1i syl5cbir wfo f1ofo foima syl
      orim12d visset elpr cvv imaexg ax-mp 3imtr4g r19.21aiv wi syl6eq wf f1of
      fimacnv cnvex 3jca ctop wb indistop cuni cun 0ex elisseti unipr uncom
      un0 3eqtrri ishomeo mp3an sylibr eximi bren hmph mp2an 3imtr4i ) ABGIZJZG
      KWJLAMZLBMZNOPZGKZABQRWLWMSRZWKWNGWKWKWJHIZTZWMPZHWLUAZWJUBZWQTZWLPZHWMUA
      ZUCZWNWKWKWTXDWKUDWKWSHWLWKWQLUEZWQAUEZUFWRLUEZWRBUEZUFWQWLPWSWKXFXHXGXIX
      FXHWJLTZLUEZWKXFWRXJLWQLWJUGUHXKWKWJUIUJUKXGXIWJATZBUEZWKXGWRXLBWQAWJUGUH
      WKABWJULXMABWJUMABWJUNUOUKUPWQLAHUQZURWRLBWJUSPZWRUSPGUQZWJWQUSUTVAURVBVC
      WKXCHWMWKXFWQBUEZUFXBLUEZXBAUEZUFWQWMPXCWKXFXRXQXSXFXRVDWKXFXBXALTLWQLXAU
      GXAUIVEUJXQXSXABTZAUEZWKXQXBXTAWQBXAUGUHWKABWJVFYAABWJVGABWJVHUOUKUPWQLBX
      NURXBLAXAUSPXBUSPWJXPVIXAWQUSUTVAURVBVCVJWLVKPZWMVKPZXOWNXEVLAVMZBVMZXPHU
      SWJWLWMABWLVNLAVOALVOALAVPACEVQVRLAVSAVTWAWMVNLBVOBLVOBLBVPBDFVQZVRLBVSBV
      TWAWBWCWDWEABGYFWFYBYCWPWOVLYDYEGWLWMWGWHWI $.
      $( [1-Sep-2008] $) $( [17-Aug-2008] $)
  $}

  $( ` { (/) } ` is the only topology with one element. $)
  top1 $p |- ( J e. Top -> ( J ~~ 1o <-> J = { (/) } ) ) $=
    ( ctop wcel c1o cen wbr c0 csn wceq wi 0opn setwoe ex syl 0ex ensn1 breq1
    mpbiri impbid1 ) ABCZADEFZAGHZIZTGACZUAUCJAKUDUAUCGALMNUCUAUBDEFGOPAUBDEQRS
    $.
    $( [1-Sep-2008] $) $( [18-Aug-2008] $)

  $( If a topology has two element it is the indiscrete topology. $)
  top2ind $p |- ( ( J e. Top /\ J ~~ 2o ) -> J = { (/) , U. J } ) $=
    ( ctop wcel c2o cen wbr c0 cuni cpr wceq wi 0opn csn wb 0top wn c1o 0ex
    ensn1 breq1 mpbiri csdm 1sdom2 wa ensdomtr sdomnen syl mpan2 con2i bibif
    wne df-ne necom set2elt 3exp com24 com12 sylbi sylbir syl6bi com3r mpd
    com4l eqid topopn syl5 mpid pm2.43i imp ) ABCZADEFZAGAHZIJZVJVKVMKZVJVJGACZ
    VNALVJVLACZVOVNKZVJVJVLGJZAGMZJZNZVPVQKAOVKWAVPVOVMVKVTPZWAVPVOVMKKZKVTVKVT
    AQEFZVKPZVTWDVSQEFGRSAVSQETUAWDQDUBFZWEUCWDWFUDADUBFWEAQDUEADUFUGUHUGUIWBWA
    VKWCWBWAVRPZVKWCKZVRVTUJWGVLGUKZWHVLGULWIGVLUKZWHVLGUMVKWJWCVKVOVPWJVMVKVOV
    PWJVMKGVLAUNUOUPUQURUSUTVAVBVCUGAVLVLVDVEVFVGVHVI $.
    $( [1-Sep-2008] $) $( [11-Aug-2008] $)

  $( If a topology has two elements its underlying set can't be empty. $)
  top2usne $p |- ( ( J e. Top /\ J ~~ 2o ) -> U. J =/= (/) ) $=
    ( ctop wcel c2o cen wbr wa cuni c0 wceq wn wne cpr wi top2ind csn wss
    uni0b wo sssn breq1 csuc sssucid sstr nsuceq0 necomi wpss df-pss com
    peano1 peano2 ax-mp php ensymg nsyl mpan sylbir mpan2 syl mp2an c1o df-2o
    df-1o suceq eqtri breq2i mtbir pm2.21i syl6bi a1i com13 adantl com12 0ex
    ensn1 2onn entr cvv 1onn elisseti php5 syl6 a1d ex eqcoms mpi jaoi sylbi
    mpcom pm2.01d df-ne sylibr ) ABCZADEFZGZAHZIJZKZWPILWOWQAIWPMJZWOWQWRNAOWQW
    OWSWRWQAIPZQZWOWSWRNZNZARXAAIJZAWTJZSXCAITXDXCXEWOXDXBWNXDXBNWMWSXDWNWRXDWN
    WRNNWSXDWNIDEFZWRAIDEUAXFWRXFIIUBZUBZEFZIXGQZXGXHQZXIKZIUCXGUCXJXKGIXHQZXLI
    XGXHUDXMIXHLZXLXHIXGUEUFXMXNGIXHUGZXLIXHUHXHUICZXOXLXGUICZXPIUICXQUJIUKULXG
    UKULZXPXOGXHIEFZXIXHIUMXPXIXSNXRIXHUIUNULUOUPUQURUSUTDXHIEDVAUBZXHVBVAXGJXT
    XHJVCVAXGVDULVEVFVGVHVIVJVKVLVMXEWTVAEFZXCIVNVOYAXCNWTAWTAJYAAVAEFZXCWTAVAE
    UAWOYBXBWNYBXBNZWMDUICZWNYCNVPYDWNDAEFZYCADUIUNYEYBXBYEYBGDVAEFZXBDAVAVQYFW
    RWSVAVRCZYFWRNVAUIVSVTYGYFVADEFZWRDVAVRUNYHWRYHVAXTEFZVAUICYIKVSVAWAULDXTVA
    EVBVFVGVHWBULWCUSWDWBULVLVMVIWEWFWGWHWHVKWIWJWPIWKWL $.
    $( [1-Sep-2008] $) $( [11-Aug-2008] $)

  $( An unnamed topological preservation theorem. $)
  homindlem2 $p |- ( ( K e. Top /\ K ~= { (/) } ) -> K = { (/) } ) $=
    ( ctop wcel c0 csn chomeo wbr wa c1o cen wceq wi sn0top homcard mpan2 imp
    0ex ensn1 jctir entr syl wb top1 adantr mpbid ) ABCZADEZFGZHZAIJGZAUGKZUIAU
    GJGZUGIJGZHUJUIULUMUFUHULUFUGBCUHULLMAUGNOPDQRSAUGITUAUFUJUKUBUHAUCUDUE $.
    $( [1-Sep-2008] $) $( [18-Aug-2008] $)

  $( Homeomorphisms preserve topological indiscretion. $)
  homindlem3 $p |- ( ( K e. Top /\ A =/= (/) /\ A e. B ) -> ( { (/) , A } ~= K
     -> K = { (/) , U. K } ) ) $=
    ( ctop wcel c0 wne w3a cpr chomeo wbr c2o cen wa cuni wceq simpl1 wi cvv
    elisset cif preq2 eleq1d indistop dedth syl hmphsyma homcard ancoms syld
    ex com12 a1d 3imp1 0ex necom unpde2eg2 3exp com13 sylbi mpii a1i 3imp
    adantr entr syl11anc jca top2ind syl6 ) CDEZAFGZABEZHZFAIZCJKZVJCLMKZNZCFCO
    IPVMVOVQVMVONZVJVPVJVKVLVOQVRCVNMKZVNLMKZVPVJVKVLVOVSVJVLVOVSRZRVKVLVJWAVLV
    NDEZVJWARVLASEZWBABTWCWBFWCAFUAZIZDEAFAWDPVNWEDAWDFUBUCWDUDUEUFWBVJWAWBVJNV
    OCVNJKZVSVNCUGVJWBWFVSRCVNUHUIUJUKUFULUMUNVMVTVOVJVKVLVTVKVLVTRRVJVKVLFSEZV
    TUOVKFAGZVLWGVTRRAFUPWGVLWHVTWGVLWHVTFASBUQURUSUTVAVBVCVDCVNLVEVFVGUKCVHVI
    $.
    $( [1-Sep-2008] $) $( [18-Aug-2008] $)


