$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( In this section every @d statement has been checked. 10-Feb-2008 $)

  ${
    $d F x y $.  $d G x y $.  $d J x y $.  $d K x y $.  $d L x y $.
    $( The composite of two homeomorphisms is a homeomorphism.  (Contributed by
       FL, 9-Mar-2007.) $)
    cmphmp $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
     ( ( F e. ( J Homeo K ) /\ G e. ( K Homeo L ) ) ->
     ( G o. F ) e. ( J Homeo L ) ) ) $=
      ( vx vy ctop wcel chomeosm co wa cuni wf1o cima wral ccnv wi imp syl ccom
      w3a cv eqid hmeomap 3adant3 3adant1 anim12d pm3.22 f1oco 3syl hbra1 ax-17
      hmeocnb hbral hban df-ral pm3.35 wceq imaeq2 eleq1d rcla4v expimpd eqcomi
      wal imaco eleq1i biimpi syl6com ex a4s sylbi ralrimi hmeocna cnvco eqtr2i
      imaeq1i impcom 3jca cvv coexg ancoms ishomeo 3expia 3adant2 syl5 mpbird
      wb ) CHIZDHIZEHIZUBZACDJKZIZBDEJKZIZLZBAUAZCEJKIZWLWQLZWSCMZEMZWRNZWRFUCZ
      OZEIZFCPZWRQZXDOZCIZFEPZUBZWTXCXGXKWTXADMZANZXMXBBNZLZXOXNLXCWLWQXPWLWNXN
      WPXOWIWJWNXNRWKACDXAXMXAUDZXMUDZUEUFWJWKWPXORWIBDEXMXBXRXBUDZUEUGUHSXNXOU
      IXAXMXBBAUJUKWTAXDOZDIZFCPZBGUCZOZEIZGDPZLZXGWLWQYGWLWNYBWPYFWIWJWNYBRWKF
      ACDUNUFWJWKWPYFRWIGBDEUNUGUHSYGXFFCYBYFFYAFCULYEFGDYCDIFUMYEFUMUOUPYBYFXD
      CIZXFRZYBYHYARZFVEYFYIRZYAFCUQYJYKFYJYFYIYHYJYFLBXTOZEIZXFYHYJYFYMYHYJLYA
      YFYMRYHYAURYEYMGXTDYCXTUSYDYLEYCXTBUTVAVBTVCYMXFYLXEEXEYLBAXDVFVDVGVHVIVJ
      VKVLSVMTWTAQZYCOZCIZGDPZBQZXDOZDIZFEPZLZXKWLWQUUBWLWNYQWPUUAWIWJWNYQRWKGA
      CDVNUFWJWKWPUUARWIFBDEVNUGUHSUUBXJFEYQUUAFYQFUMYTFEULUPUUAYQXDEIZXJRZUUAU
      UCYTRZFVEYQUUDRZYTFEUQUUEUUFFUUEYQUUDUUCUUEYQLYNYSOZCIZXJUUCUUEYQUUHUUCUU
      ELYTYQUUHRUUCYTURYPUUHGYSDYCYSUSYOUUGCYCYSYNUTVAVBTVCUUHXJUUGXICXIYNYRUAZ
      XDOUUGXHUUIXDBAVOVQYNYRXDVFVPVGVHVIVJVKVLVRVMTVSWLWQWSXLWHZWQWRVTIZWLUUJW
      PWNUUKBAWOWMWAWBWIWKUUKUUJRWJWIWKUUKUUJFVTWRCEXAXBXQXSWCWDWEWFSWGVJ $.
      $( [11-Mar-2007] $) $( [9-Mar-2007] $)
  $}

  ${
    $d J x $.  $d X x $.
    idhme.1 $e |- X = U. J $.
    $( The identity function is a homeomorphism.  (Contributed by FL,
       14-Feb-2007.) $)
    idhme $p |- ( J e. Top -> ( _I |` X ) e. ( J Homeo J ) ) $=
      ( vx ctop wcel cid cres cvv chomeosm syl w3a cima wral a1i wss id eqeltrd
      wceq rgen co cuni syl5eqel resiexg wf1o cv ccnv elssuni syl6sseqr resiima
      uniexg f1oi 3syl cnvresid imaeq1d eqtrd ishomeo mpbird 3anidm12 mpdan
      3jca ) AEFZGBHZIFZVCAAJUAFZVBBIFVDVBBAUBZICAEUKUCBIUDKVBVDVEVBVBVDLZVEBBV
      CUEZVCDUFZMZAFZDANZVCUGZVIMZAFZDANZLVGVHVLVPVHVGBULOVLVGVKDAVIAFZVJVIAVQV
      IVFPZVIBPZVJVISVIAUHZVRVIVFBVRQCUIZBVIUJZUMVQQZRTOVPVGVODAVQVNVIAVQVRVNVI
      SZVTVRVSWDWAVSVNVJVIVSVMVCVIVMVCSVSBUNOUOWBUPKKWCRTOVADIVCAABBCCUQURUSUT
      $.
      $( [20-Feb-2007] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The converse of a homeomorphism is a homeomorphism.  (Contributed by FL,
       5-Mar-2007.) $)
    cnvhmphb $p |- ( ( J e. Top /\ K e. Top /\ Rel F ) ->
      ( `' F e. ( J Homeo K ) -> F e. ( K Homeo J ) ) ) $=
      ( vx ccnv chomeosm co wcel ctop w3a wi wa eleq1d cuni wf1o cima wral eqid
      cvv syl9 wrel cnvexg wceq dfrel2 cv hmeomap f1ocnvb biimprd imp31 hmeocna
      biimpi imaeq1d ralbidv biimpd hmeocnb adantr 3jca exp31 ancoms 3adant3 wb
      imp ishomeo sylibrd 3exp1 impcom com3l syl6bi pm2.43a com3r 3impia mpcom
      com12 ) AEZBCFGZHZBIHZCIHZAUAZJZACBFGHZVNEZSHZVPVTWAKVNVOUBVTWCVPWAVQVRVS
      WCVPWAKZKVSWCVQVRLZWDWCVSWEWDKZVSWCASHZVSWFKVSWBASVSWBAUCAUDUKZMWEWGVSWDV
      RVQWGVSWDKKVRVQWGVSWDVRVQWGJZVSLVPCNZBNZAOZADUEZPZBHZDCQZVNWMPCHDBQZJZWAW
      IVSVPWRKZVRVQVSWSKZWGVQVRWTWEVSVPWRWEVSLZVPLWLWPWQWEVSVPWLWEVPWKWJVNOZVSW
      LVNBCWKWJWKRZWJRZUFVSWLXBWJWKAUGUHTUIWEVSVPWPWEVPWBWMPZBHZDCQZVSWPDVNBCUJ
      VSXGWPVSXFWODCVSXEWNBVSWBAWMWHULMUMUNTUIXAVPWQWEVPWQKVSDVNBCUOUPVBUQURUSU
      TVBWIWAWRVAVSDSACBWJWKXDXCVCUPVDVEVFVGVHVIVJVKVGVLVM $.
      $( [11-Mar-2007] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F j $.  $d J j $.  $d K j $.
    $( The converse of a homeomorphism is a homeomorphism.  (Contributed by FL,
       15-Feb-2007.) $)
    cnvhmph $p |- ( ( ( J e. Top /\ K e. Top /\ F e. A ) /\ Rel F ) ->
      ( F e. ( J Homeo K ) <-> `' F e. ( K Homeo J ) ) ) $=
      ( vj ctop wcel w3a cuni ccnv wf1o cima wral chomeosm co adantl cvv adantr
      wb id wrel wa cv 3ancomb f1ocnvb bicomd wceq dfrel2 biimpi imaeq1d eleq1d
      ralbidv 3anbi12d syl5bb cnvexg 3anim123i 3com12 eqid ishomeo syl 3bitr4rd
      ) CFGZDFGZBAGZHZBUAZUBZDIZCIZBJZKZVJEUCZLCGEDMZVJJZVLLZDGZECMZHZVIVHBKZBV
      LLZDGZECMZVMHZVJDCNOGZBCDNOGZVRVKVQVMHVGWCVKVMVQUDVGVKVSVQWBVMVFVKVSSVEVF
      VSVKVIVHBUEUFPVFVQWBSVEVFVPWAECVFVOVTDVFVNBVLVFVNBUGBUHUIUJUKULPUMUNVGVCV
      BVJQGZHZWDVRSVEWGVFVCVBVDWGVCVCVBVBVDWFVCTVBTBAUOUPUQREQVJDCVHVIVHURZVIUR
      ZUSUTVEWEWCSVFEABCDVIVHWIWHUSRVA $.
      $( [5-Mar-2007] $) $( [15-Feb-2007] $)
  $}

  ${
    $d J f g $.  $d J f x $.  $d K f g $.  $d K f x $.
    $( "Is homeomorph to" is symmetric.  (Contributed by FL, 8-Mar-2007.) $)
    hmphsyma $p |- ( ( J e. Top /\ K e. Top ) ->
        ( J ~= K -> K ~= J ) ) $=
      ( vf vg vx ctop wcel wa cv chomeosm wex chomeo wbr cvv w3a cuni cima wral
      co wi visset wf1o ccnv eqid cnvexg 3ad2ant3 adantr f1orel 3ad2ant1 adantl
      ishomeo wrel f1ocnv a1i wceq dfrel2 biimpi eqcomd imaeq1d eleq1d ad2antrr
      biimpd ralimdva idd 3anim123d expimpd mpcom 3ancomb syl3an3 3com12 mpbird
      sylib wb eleq1 cla4egv sylc ex sylbid mp3an3 exlimdv hmph ancoms 3imtr4d
      ) AFGZBFGZHZCIZABJSGZCKDIZBAJSZGZDKZABLMBALMZWFWHWLCWDWEWGNGZWHWLTCUAWDWE
      WNOZWHAPZBPZWGUBZWGEIZQZBGZEARZWGUCZWSQAGEBRZOZWLENWGABWPWQWPUDZWQUDZUKWO
      XEWLWOXEHZXCNGZXCWJGZWLWOXIXEWNWDXIWEWGNUEZUFUGXHXJWQWPXCUBZXDXCUCZWSQZBG
      ZEARZOZXHXLXPXDOZXQWGULZXHXRXEXSWOWRXBXSXDWPWQWGUHUIUJXSWOXEXRXSWOHZWRXLX
      BXPXDXDWRXLTXTWPWQWGUMUNXTXAXOEAXSXAXOTWOWSAGXSXAXOXSWTXNBXSWGXMWSXSXMWGX
      SXMWGUOWGUPUQURUSUTVBVAVCXTXDVDVEVFVGXLXPXDVHVLWOXJXQVMZXEWEWDWNYAWNWEWDX
      IYAXKENXCBAWQWPXGXFUKVIVJUGVKWKXJDXCNWIXCWJVNVOVPVQVRVSVTCABWAWEWDWMWLVMD
      BAWAWBWC $.
      $( [11-Mar-2007] $) $( [8-Mar-2007] $)
  $}


  ${
    $( "Is homeomorph to" is symmetric.  (Contributed by FL, 22-Feb-2007.) $)
    hmphsym $p |- ( ( J e. Top /\ K e. Top ) ->
        ( J ~= K <-> K ~= J ) ) $=
      ( ctop wcel wa chomeo wbr hmphsyma wi ancoms impbid ) ACDZBCDZEABFGZBAFGZ
      ABHMLONIBAHJK $.
      $( [11-Mar-2007] $) $( [22-Feb-2007] $)
  $}

  ${
    $d J f $.  $d J x $.
    $( "Is homeomorph to" is reflexive.  (Contributed by FL, 25-Feb-2007.) $)
    hmphre $p |- ( J e. Top -> J ~= J ) $=
      ( vf vx ctop wcel chomeo wa cv wex cvv wi w3a cima wral a1i mpd rgen wceq
      mpbird syl wbr chomeosm co cuni uniexg cid cres resiexg wf1o ccnv elssuni
      f1oi wss resiima eleq1d biimprcd cnvresid imaeq1i adantl eqeltrd syl5eqel
      simpl mpdan 3jca eqid ishomeo 3exp pm2.43i com12 eleq1 imbi2d cla4egv imp
      19.37v sylib hmph anidms ) ADEZAAFUAZVRVRGVSBHZAAUBUCZEZBIZVRVRWCVRAUDZJE
      ZVRWCKZADUEWEUFWDUGZJEZWFWDJUHWHVRWBKZBIZWFWHVRWGWAEZKZWJVRWHWKVRWHWKKVRV
      RWHWKVRVRWHLZWKWDWDWGUIZWGCHZMZAEZCANZWGUJZWOMZAEZCANZLWMWNWRXBWNWMWDULOW
      RWMWQCAWOAEZWOWDUMZWQWOAUKZXDWQXCXDWPWOAWDWOUNZUOUPPQOXBWMXACAXCXDXAXEXCX
      DGZWTWPAWSWGWOWDUQURXGWPWOAXDWPWORXCXFUSXCXDVBUTVAVCQOVDCJWGAAWDWDWDVEZXH
      VFSVGVHVIWIWLBWGJVTWGRWBWKVRVTWGWAVJVKVLPVRWBBVNVOTTVMBAAVPSVQ $.
      $( [5-Mar-2007] $) $( [25-Feb-2007] $)
  $}

  ${
    $d J f g h $.  $d K f g $.  $d L f g h $.
    $( "Is homeomorph to" is transitive.  (Contributed by FL, 9-Mar-2007.) $)
    hmphtr $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( J ~= K /\ K ~= L ) -> J ~= L ) ) $=
      ( vf vg vh ctop wcel chomeo wbr cv chomeosm co wex wi hmph biimpd exlimdv
      wa cvv w3a 3adant3 3adant1 ccom coexg adantl cmphmp ancomsd eleq1 cla4egv
      imp sylc exp32 com23 imp3a wb 3adant2 sylibrd syl2and ) AGHZBGHZCGHZUAZAB
      IJZDKZABLMZHZDNZBCIJZEKZBCLMZHZENZACIJZUTVAVDVHOVBUTVASVDVHDABPQUBVAVBVIV
      MOUTVAVBSVIVMEBCPQUCVCVHVMSFKZACLMZHZFNZVNVCVHVMVRVCVGVMVRODVCVMVGVRVCVLV
      GVROEVCVLVGVRVCVLVGSZSVJVEUDZTHZVTVPHZVRVSWAVCVJVEVKVFUEUFVCVSWBVCVGVLWBV
      EVJABCUGUHUKVQWBFVTTVOVTVPUIUJULUMRUNRUOUTVBVNVRUPVAFACPUQURUS $.
      $( [9-Mar-2007] $)
  $}

  ${
    $d x y z $.
    $( ` ~= ` is a relation whose domain is included in ` Top ` .  (Contributed
       by FL, 23-Mar-2007.) $)
    dmhmph $p |- dom ~= C_ Top $=
      ( vx vy vz chomeo cdm ctop cxp wss cv wcel wa chomeosm co wex w3a df-hmph
      copab df-3an opabbii eqtri opabssxp eqsstri dmss ax-mp dmxpid sseqtri ) D
      EZFFGZEZFDUHHUGUIHDAIZFJZBIZFJZKCIUJULLMJCNZKZABQZUHDUKUMUNOZABQUPABCPUQU
      OABUKUMUNRSTUNABFFUAUBDUHUCUDFUEUF $.
      $( [23-Mar-2007] $)

    $( ` ~= ` is a relation whose range is included in ` Top ` .  (Contributed
       by FL, 23-Mar-2007.) $)
    rnhmph $p |- ran ~= C_ Top $=
      ( vx vy vz chomeo crn ctop cxp wss cv wcel wa chomeosm co wex w3a df-hmph
      copab df-3an opabbii eqtri opabssxp eqsstri rnss ax-mp rnxpss sstri ) DEZ
      FFGZEZFDUHHUGUIHDAIZFJZBIZFJZKCIUJULLMJCNZKZABQZUHDUKUMUNOZABQUPABCPUQUOA
      BUKUMUNRSTUNABFFUAUBDUHUCUDFFUEUF $.
      $( [23-Mar-2007] $)
  $}

  ${
    dmhmpha.1 $e |- A e. _V $.
    $( The relation "being homeomorph to" implies the operands are topologies.
       (Contributed by FL, 23-Mar-2007.) $)
    dmhmpha $p |- ( A ~= B -> A e. Top ) $=
      ( chomeo wbr cdm wcel ctop breldm dmhmph sseli syl ) ABDEADFZGAHGABDCIMHA
      JKL $.
      $( [23-Mar-2007] $)
  $}

  ${
    rnhmpha.1 $e |- A e. _V $.
    rnhmpha.2 $e |- B e. _V $.
    $( The relation "being homeomorph to" implies the operands are topologies.
       (Contributed by FL, 23-Mar-2007.) $)
    rnhmpha $p |- ( A ~= B -> B e. Top ) $=
      ( chomeo wbr crn wcel ctop brelrn rnhmph sseli syl ) ABEFBEGZHBIHABECDJNI
      BKLM $.
      $( [23-Mar-2007] $)
  $}


  ${
    $d x y z $.
    $( "Is homeomorph to" is an equivalence relation.  (Contributed by FL,
       22-Mar-2007.) $)
    hmpher $p |- Er ~= $=
      ( vx vy vz chomeo cv ctop wcel wa wbr visset dmhmpha rnhmpha jca hmphsyma
      mpcom w3a adantr adantl 3jca hmphtr ster ) ABCDAEZFGZBEZFGZHUBUDDIZUDUBDI
      UFUCUEUBUDAJZKZUBUDUGBJZLMUBUDNOUCUECEZFGZPUFUDUJDIZHZUBUJDIUMUCUEUKUFUCU
      LUHQULUEUFUDUJUIKRULUKUFUDUJUICJLRSUBUDUJTOUA $.
      $( [22-Mar-2007] $)
  $}

  ${
    hmphsymv.1 $e |- A e. _V $.
    hmphsymv.2 $e |- B e. _V $.
    $( A more general version of ~ hmphsym .  Certainly not a useful proof
       (since it's a simple consequence of ~ hmpher ) but it shows that the
       conditions of ~ hmphsym could (and should) be weakened.  (Contributed by
       FL, 23-Mar-2007.) $)
    hmphsymv $p |- ( A ~= B <-> B ~= A ) $=
      ( chomeo hmpher ersymb ) ABECDFG $.
      $( [23-Mar-2007] $)

  $}

  ${
    $d J a b c j x y z $.  $d f t u v w $.  $d v w x y z $.
    $( Homeomorphisms on a topology ` J ` is a group for composition.  This
       means from Felix Klein's point of view that a set equipped with a
       topology is a geometry, namely the so-called rubber sheet geometry.
       (Contributed by FL, 3-Feb-2008.) $)
    hmeogrp $p |- ( J e. Top -> { <. <. x , y >. , z >. | ( x e. ( J Homeo J )
    /\ y e. ( J Homeo J ) /\ z = ( x o. y ) ) } e. GrpOp ) $=
      ( va vb vv ctop wcel cv co ccom wceq w3a wral wa wi cvv coeq2 c0 vc vj vw
      chomeosm copab2 cgr wf wrex cmphmp ancomsd 3exp pm2.43d pm2.43i ralrimivv
      cxp df-3an oprabbii foprab2 sylib coass simpr1 simpr2 coexg 3adant3 coeq1
      adantl oprabval2g opreq1d com12 ancoms impcom simpr3 simp3 syl11anc eqtrd
      syl111anc 3adant1 opreq2d 3eqtr4a 3exp2 imp32 ralrimiv cid cuni cres eqid
      ex idhme adantr simpr exp3a mpid wf1o hmeomap anidms f1of fcoi2 3syl ccnv
      imp cnvhmpha expdimp mpd f1ococnv1 syl opreq1 rcla4ev jca ralrimiva eqeq2
      eqeq1d rexbidv anbi12d ralbidv 3jca wb oprex wex cab eqcomd anbi2d 3anass
      syl6 bitri bicomd abbi1dv wn cima anbi1i anass abbii visset uniex pm2.21i
      crn noel impbid1 eqtr4d wrel pm2.61i pm3.2i oprabex2g eleq1a com3l expcom
      a1i 3imp exlimivv fcoi1 eqeq2d syl12anc 3ancoma exbii df-rex bitr4i eleq1
      sylibr eqeq1 3anbi3d exbidv mpbiri a4imev impbid simpl con3i cdm df-homeo
      imbi12d wfun wf1 dff1o5 df-f1 3bitri fabex eqeltri dmoprab2a mp3an1 simp1
      ndmoprg eleq2 syl6bi syl5 exlimdvv relxp releqi mpbir oprprc1 a1d rnoprab
      eqtr4i isgrpo mpbird ) DHIZAJZDDUDKZIZBJZUWOIZCJZUWNUWQLZMZNZABCUEZUFIZUW
      OUWOUOUWOUXCUGZEJZFJZUXCKZUAJZUXCKZUXFUXGUXIUXCKZUXCKZMZUAUWOOZFUWOOEUWOO
      ZUBJZUXFUXCKZUXFMZUXGUXFUXCKZUXPMZFUWOUHZPZEUWOOZUBUWOUHZNZUWMUXEUXOUYDUW
      MUWTUWOIZBUWOOAUWOOUXEUWMUYFABUWOUWOUWMUWPUWRPZUYFQZUWMUWMUYHUWMUWMUWMUYH
      UWMUWMUWMNZUWRUWPUYFUWQUWNDDDUIZUJUKULUMUNABCUWOUWOUWTUWOUXCUXBUYGUXAPABC
      UWPUWRUXAUPUQZURUSUWMUXNEFUWOUWOUWMUXFUWOIZUXGUWOIZPZUXNUWMUYNPUXMUAUWOUW
      MUYLUYMUXIUWOIZUXMQUWMUYLUYMUYOUXMUWMUYLUYMUYONZPZUXFUXGLZUXILZUXFUXGUXIL
      ZLZUXJUXLUXFUXGUXIUTUYQUXJUYRUXIUXCKZUYSUYQUYLUYMUYRRIZUXJVUBMUWMUYLUYMUY
      OVAZUWMUYLUYMUYOVBZUYPVUCUWMUYLUYMVUCUYOUXFUXGUWOUWOVCVDZVFUYLUYMVUCNUXHU
      YRUXIUXCABCUXFUXGUWOUWOUWTUYRUXCUXFUWQLZRUWNUXFUWQVEZUWQUXGUXFSUYKVGVHVPU
      YQUYRUWOIZUYOUYSRIZVUBUYSMUYPUWMVUIUYLUYMUWMVUIQZUYOUYMUYLVUKUWMUYMUYLPZV
      UIUWMVULVUIQZUWMUWMVUMUWMUWMUWMVUMUXGUXFDDDUIUKULUMVIVJVDVKUWMUYLUYMUYOVL
      ZUYPVUJUWMUYPVUCUYOVUJVUFUYLUYMUYOVMUYRUXIRUWOVCVNVFABCUYRUXIUWOUWOUWTUYS
      UXCUYRUWQLRUWNUYRUWQVEUWQUXIUYRSUYKVGVPVOUYQUXLUXFUYTUXCKZVUAUYQUXKUYTUXF
      UXCUYQUYMUYOUYTUWOIZUXKUYTMVUEVUNUYPUWMVUPUYMUYOUWMVUPQZUYLUYOUYMVUQUWMUY
      OUYMPZVUPUWMVURVUPQZUWMUWMVUSUWMUWMUWMVUSUXIUXGDDDUIUKULUMVIVJVQVKZABCUXG
      UXIUWOUWOUWTUYTUXCUXGUWQLUWOUWNUXGUWQVEUWQUXIUXGSUYKVGVPVRUYQUYLVUPVUARIZ
      VUOVUAMVUDVUTUYQUYLUYTRIZVVAVUDUYPVVBUWMUYMUYOVVBUYLUXGUXIUWOUWOVCVQVFUXF
      UYTUWORVCVNABCUXFUYTUWOUWOUWTVUAUXCVUGRVUHUWQUYTUXFSUYKVGVPVOVSVTWAWBWGUN
      UWMWCDWDZWEZUWOIZVVDUXFUXCKZUXFMZUXSVVDMZFUWOUHZPZEUWOOZUYDDVVCVVCWFZWHZU
      WMVVJEUWOUWMUYLPZVVGVVIVVNVVFVVDUXFLZUXFVVNVVEUYLVVOUWOIZVVFVVOMUWMVVEUYL
      VVMWIUWMUYLWJZUWMUYLVVPUWMUYLVVEVVPVVMUWMUYLVVEVVPQQZUWMUWMVVRUWMUWMUWMVV
      RUYIUYLVVEVVPUXFVVDDDDUIWKUKULUMWLWTABCVVDUXFUWOUWOUWTVVOUXCVVDUWQLUWOUWN
      VVDUWQVEUWQUXFVVDSUYKVGVPVVNVVCVVCUXFWMZVVCVVCUXFUGVVOUXFMUWMUYLVVSUWMUYL
      VVSQUXFDDVVCVVCVVLVVLWNWOWTZVVCVVCUXFWPVVCVVCUXFWQWRVOVVNUXFWSZUWOIZVWAUX
      FUXCKZVVDMZVVIUWMUYLVWBUWMUYLVWBQUXFDDXAWOWTZVVNVWCVWAUXFLZVVDVVNVWBUYLVW
      FUWOIZVWCVWFMVWEVVQVVNVWBVWGVWEUWMUYLVWBVWGUWMUYLVWBPVWGQZUWMUWMVWHUWMUWM
      UWMVWHUXFVWADDDUIUKULUMXBXCABCVWAUXFUWOUWOUWTVWFUXCVWAUWQLUWOUWNVWAUWQVEU
      WQUXFVWASUYKVGVPVVNVVSVWFVVDMVVTVVCVVCUXFXDXEVOVVHVWDFVWAUWOUXGVWAMUXSVWC
      VVDUXGVWAUXFUXCXFXKXGVNXHXIUYCVVKUBVVDUWOUXPVVDMZUYBVVJEUWOVWIUXRVVGUYAVV
      IVWIUXQVVFUXFUXPVVDUXFUXCXFXKVWIUXTVVHFUWOUXPVVDUXSXJXLXMXNXGVNXOUWMUWORI
      ZVWJPZUXCRIUXDUYEXPVWKUWMVWJVWJDDUDXQZVWLUUAUUFABCUWOUWOUWTRRUXCUYKUUBEFU
      AUBRUXCUWOUWOUXBBXRZAXRZCXSZUXCYOUWMUWOVWOMZUWMVWOUWOUWMVWNCUWOUWMVWNUWSU
      WOIZVWNUWMVWQUXBUWMVWQQZABUWPUWRUXAVWRUWRUWPUXAVWRQUWMUWRUWPPZUXAVWQUWMVW
      SUXAVWQQZQZUWMUWMVXAUWMUWMUWMVXAUYIVWSUYFVWTUYJUWTUWOUWSUUCYCUKULUMUUDUUE
      UUGUUHVIUWMVWQVWNUWMVWQPZVWMACUWNUWSMZVXBVWMQUWMUWPPZUWPUWRUWNUWTMZNZBXRZ
      QVXDUWPVXEPZBUWOUHZVXGVXDVVEUWPUWNUWNVVDLZMZVXIUWMVVEUWPVVMWIUWMUWPWJUWMU
      WPVXKUWMUWPVXKQUWMUWMPZUWPVVCVVCUWNWMZVXKUWNDDVVCVVCVVLVVLWNVXMVXJUWNVXMV
      VCVVCUWNUGVXJUWNMVVCVVCUWNWPVVCVVCUWNUUIXEXTYCWOWTVXHUWPVXKPBVVDUWOUWQVVD
      MZVXEVXKUWPVXNUWTVXJUWNUWQVVDUWNSUUJYAXGUUKVXGUWRVXHPZBXRVXIVXFVXOBVXFUWR
      UWPVXENVXOUWPUWRVXEUULUWRUWPVXEYBYDUUMVXHBUWOUUNUUOUUQVXCVXBVXDVWMVXGVXCV
      WQUWPUWMVXCUWPVWQUWNUWSUWOUUPYEYAVXCUXBVXFBVXCUXAVXEUWPUWRVXCVXEUXAUWNUWS
      UWTUURYEUUSUUTUVHUVAUVBWGUVCYFXTUWMYGVXLYGZVWPVXLUWMUWMUWMUVDUVEDRIZVXPVW
      PQVXQVXPVWPVXQVXPPZUWOTVWOUDUVFZHHUOZMVXQVXPUWOTMZABHHUWNWDZUWQWDZGJZWMZV
      YDUCJZYHUWQIUCUWNOZVYDWSZVYFYHUWNIUCUWQOZNZGXSZUDUCGABUVGVYKVYBVYCVYDUGZV
      YHUVIZVYDYOVYCMZPZPZVYGVYIPZPZGXSZRVYJVYRGVYJVYEVYQPVYRVYEVYGVYIYBVYEVYPV
      YQVYEVYBVYCVYDUVJZVYNPVYLVYMPZVYNPVYPVYBVYCVYDUVKVYTWUAVYNVYBVYCVYDUVLYIV
      YLVYMVYNYJUVMYIYDYKVYOVYQPZGVYBVYCVYSUWNAYLYMUWQBYLYMVYRVYLWUBPGVYLVYOVYQ
      YJYKUVNUVOUVPZDDRHHUDUVSUVQZVXRVWNCTVXRVWNUWSTIZVXRVYAVWNWUEQZWUDVYAUXBWU
      EABUXBUWPVYAWUEUWPUWRUXAUVRVYAUWPUWNTIZWUEUWOTUWNUVTWUGWUEUWNYPYNUWAUWBUW
      CZXEWUEVWNUWSYPYNZYQYFYRWGVXQYGZVWPVXPWUJUWOTVWODDUDVXSYSVXTYSHHUWDVXSVXT
      WUCUWEUWFUWGZWUJVWNCTWUJVWNWUEWUJVYAWUFWUKWUHXEWUIYQYFYRUWHYTXEYTUXBABCUW
      IUWJUWKWRUWL $.
      $( [15-Dec-2013] $) $( [3-Feb-2008] $)
  $}

  ${
    $d J a b f g $.  $d J a b f m n x $.  $d K a b f g $.  $d K a b f m n x $.
    $( Homeomorphisms preserve the cardinality of the topologies.  (Contributed
       by FL, 1-Jun-2008.) $)
    homcard $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K ->
    J ~~ K ) ) $=
      ( vf vx va vb wcel wa cv wi cima wral wceq cvv wss wal com12 sylibr syl
      ex vg vm vn ctop chomeo wbr chomeosm wex cen hmph w3a cuni wf1o ccnv eqid
      co ishomeo copab opabex2g 3ad2ant1 adantr wf1 crn wf cfv wfn visset ax-mp
      imaexg a1i ralrimiva fnopab2g sylib wrex cab rnopab2 imaeq2 eleq1d expcom
      rcla4va 3ad2ant2 adantl eleq1 imbi2d syl5ibrcom rexlimiv alrimiv syl5eqss
      abss jca df-f f1of1 anim12i fvopab4g im2anan9 eqtr eqcoms eltopss anim12d
      imp3a oooeqim2 biimpd 3expib syl6 com24 com14 exp3a syl5com mp2an 3adant3
      syld impcom ralrimivv dff13 wb hbra1 ax-17 rcla4v eleq1a a1d com3l alrimi
      hban imp ra4 elssuni f2imacnv eqcom syl5bb rcla4ev imim2d pm2.43a 3adant2
      eqeq1 albiim rexbid bibi12d cbval abeq1 sylbid syl5eq dff1o5 cla4egv sylc
      f1oeq1 breng mpbird 3expia pm2.43i exlimiv ) AUDGZBUDGZHZABUEUFCIZABUGUPZ
      GZCUHZABUIUFZCABUJUUQUUMUURUUPUUMUURJZCUUPUUSUUMUUPUUPUURUUKUULUUPUUPUURJ
      UUKUULUUPUKZUUPAULZBULZUUNUMZUUNDIZKZBGZDALZUUNUNUVDKZAGZDBLZUKZUURDUUOUU
      NABUVAUVBUVAUOZUVBUOUQUUTUVKUURUUTUVKHZUURABUAIZUMZUAUHZUVMEIZAGZFIZUUNUV
      QKZMZHEFURZNGZABUWBUMZUVPUUTUWCUVKUUKUULUWCUUPEFAUVTUDUSUTVAUVMABUWBVBZUW
      BVCZBMZHUWDUVMUWEUWGUVMABUWBVDZUBIZUWBVEZUCIZUWBVEZMZUWIUWKMZJZUCALUBALZH
      UWEUVMUWHUWPUVMUWBAVFZUWFBOZHUWHUVMUWQUWRUVMUVTNGZEALUWQUVMUWSEAUWSUVMUVR
      HUUNNGZUWSCVGZUUNUVQNVIVHVJVKEFAUVTUWBUWBUOZVLVMUVMUWFUWAEAVNZFVOZBEFAUVT
      VPZUVMUXCUVSBGZJZFPUXDBOUVMUXGFUXCUVMUXFUWAUVMUXFJZEAUVRUXHUWAUVMUVTBGZJU
      VMUVRUXIUVKUVRUXIJZUUTUVGUVCUXJUVJUVRUVGUXIUVFUXIDUVQAUVDUVQMUVEUVTBUVDUV
      QUUNVQVRZVTVSWAWBQUWAUXFUXIUVMUVSUVTBWCWDWEWFQWGUXCFBWIRWHWJABUWBWKRUVMUW
      OUBUCAAUVKUUTUWIAGZUWKAGZHZUWOJZUVCUVGUUTUXOJUVJUUTUVCUXOUUKUULUVCUXOJUUP
      UVCUUMUXOUVCUVAUVBUUNVBZUUMUXOJZUVAUVBUUNWLUWTUWTUXPUXQJUXAUXAUWTUWTHUUNU
      WIKZNGZUUNUWKKZNGZHZUXPUXQUWTUXSUWTUYAUUNUWINVIUUNUWKNVIWMUXNUYBUUMUXPUWO
      UXNUYBUWJUXRMZUWLUXTMZHZUUMUXPUWOJZJUXLUXSUYCUXMUYAUYDUXLUXSUYCEFUWIUVTUX
      RANUWBUVQUWIUUNVQUXBWNTUXMUYAUYDEFUWKUVTUXTANUWBUVQUWKUUNVQUXBWNTWOUXNUYE
      UUMUYFUWMUYEUUMHUXPUXNUWNUWMUYEUUMUXPUXNUWNJJZUWMUYEUXRUXTMZUUMUYGJUWMUYC
      UYDUYHUYCUWMUYDUYHJZUWMUYIJUXRUWJUXRUWJMZUWMUYIUYJUWMHUXRUWLMZUYIUXRUWJUW
      LWPUYKUYDUYHUXRUWLUXTWPTSTWQQWTUUMUYHUYGUUMUXNUXPUYHUWNUUMUXNUWIUVAOZUWKU
      VAOZHZUXPUYHUWNJZJUUKUXNUYNJUULUUKUXLUYLUXMUYMUUKUXLUYLUWIAUVAUVLWRTUUKUX
      MUYMUWKAUVAUVLWRTWSVAUXPUYNUYOUXPUYLUYMUYOUXPUYLUYMUKUYHUWNUVAUVBUUNUWIUW
      KXAXBXCQXDXEQXDWTXFXGXKXFXHXISQXJQUTXLXMWJUBUCABUWBXNRUVMUWFUXDBUXEUVMUXC
      UXFXOZFPZUXDBMUVMUVDUVTMZEAVNZUVDBGZXOZDPZUYQUVMUYSUYTJZDPZUYTUYSJZDPZHVU
      BUVMVUDVUFUVKUUTVUDUVGUVCUUTVUDJUVJUVGUUTVUDUVGUUTHZVUCDUVGUUTDUVFDAXPUUT
      DXQYCUYSVUGUYTUYRVUGUYTJEAVUGUVRUYRUYTUVGUUTUVRUYRUYTJZJUVRUVGUUTVUHUVRUV
      GUXIUUTVUHJUVFUXIDUVQAUXKXRUXIVUHUUTUVTBUVDXSXTXDYAYDYAWFQYBTWAXLUVKVUFUU
      TUVCUVJVUFUVGUVCUVJHVUEDUVCUVJDUVCDXQUVIDBXPYCUVJUVCVUEUVJUYTUVIJZUVCVUEJ
      UVIDBYEUYTVUIUVCUYSVUIUYTUVCUYSJZUYTUVIVUJUYTUYTUVDUVBOZUVIVUJJUVDBYFUVCV
      UKUVIUYSUVCVUKUVIUYSJUVCVUKHUUNUVHKZUVDMZUVIUYSUVAUVBUVDUUNYGUVIVUMUYSUYR
      VUMEUVHAUVQUVHMUVTVULMZUYRVUMXOUVQUVHUUNVQUYRUVTUVDMVUNVUMUVDUVTYHUVTVULU
      VDYNYISYJTXHTYASYKYLYASXLYBYMWBWJUYSUYTDYORVUAUYPDFVUAFXQUYPDXQUVDUVSMZUY
      SUXCUYTUXFVUOUYRUWAEAVUOEXQUVDUVSUVTYNYPUVDUVSBWCYQYRVMUXCFBYSRUUAWJABUWB
      UUBRUVOUWDUAUWBNABUVNUWBUUEUUCUUDUUTUURUVPXOZUVKUULUUKVUPUUPABUDUAUUFWAVA
      UUGTYTUUHYAUUIUUJQYT $.
      $( [1-Jun-2008] $)
  $}

  ${
    $d J f $.  $d K f $.  $d X f $.  $d Y f $.
    homcardus.1 $e |- X = U. J $.
    homcardus.2 $e |- Y = U. K $.
    $( Homeomorphisms preserve the cardinality of the underlying sets.
       (Contributed by FL, 17-Aug-2008.) $)
    homcardus $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K -> X ~~ Y ) ) $=
      ( vf ctop wcel wa cv chomeosm co wex wf1o chomeo wbr cen hmeomap cvv hmph
      eximdv wb cuni uniexg syl5eqel adantl breng syl 3imtr4d ) AHIZBHIZJZGKZAB
      LMIZGNCDUNOZGNZABPQCDRQZUMUOUPGUNABCDEFSUBGABUAUMDTIZURUQUCULUSUKULDBUDTF
      BHUEUFUGCDTGUHUIUJ $.
      $( [1-Sep-2008] $) $( [17-Aug-2008] $)
  $}

  ${
    $d A f x $.  $d B f x $.
    eqindhome.1 $e |- A e. C $.
    eqindhome.2 $e |- B e. D $.
    $( Equinumerous sets equipped with their indiscrete topologies are
       homeomorph (which means in particular that a segment is homeomorph to a
       circle contrary to what Wikipedia claims).  (Contributed by FL,
       17-Aug-2008.) $)
    eqindhome $p |- ( A ~~ B -> { (/) , A } ~= { (/) , B } ) $=
      ( vf vx cv c0 wcel cima wceq wo imaeq2 eqeq1d syl5ibrcom elpr cvv cun wex
      wf1o cpr chomeosm co cen wbr chomeo wral ccnv w3a id ima0 a1i f1ofo foima
      wfo syl orim12d visset imaexg ax-mp 3imtr4g ralrimiv wi syl6eq wf fimacnv
      f1of cnvex 3jca ctop wb indistop 0ex elisseti unipr uncom 3eqtrri ishomeo
      cuni un0 mp3an sylibr eximi bren hmph mp2an 3imtr4i ) ABGIZUBZGUAWJJAUCZJ
      BUCZUDUEKZGUAZABUFUGWLWMUHUGZWKWNGWKWKWJHIZLZWMKZHWLUIZWJUJZWQLZWLKZHWMUI
      ZUKZWNWKWKWTXDWKULWKWSHWLWKWQJMZWQAMZNWRJMZWRBMZNWQWLKWSWKXFXHXGXIWKXHXFW
      JJLZJMZXKWKWJUMUNXFWRXJJWQJWJOPQWKXIXGWJALZBMZWKABWJUQXMABWJUOABWJUPURXGW
      RXLBWQAWJOPQUSWQJAHUTZRWRJBWJSKZWRSKGUTZWJWQSVAVBRVCVDWKXCHWMWKXFWQBMZNXB
      JMZXBAMZNWQWMKXCWKXFXRXQXSXFXRVEWKXFXBXAJLJWQJXAOXAUMVFUNWKXSXQXABLZAMZWK
      ABWJVGYAABWJVIABWJVHURXQXBXTAWQBXAOPQUSWQJBXNRXBJAXASKXBSKWJXPVJXAWQSVAVB
      RVCVDVKWLVLKZWMVLKZXOWNXEVMAVNZBVNZXPHSWJWLWMABWLWAJATAJTAJAVOACEVPVQJAVR
      AWBVSWMWAJBTBJTBJBVOBDFVPZVQJBVRBWBVSVTWCWDWEABGYFWFYBYCWPWOVMYDYEGWLWMWG
      WHWI $.
      $( [1-Sep-2008] $) $( [17-Aug-2008] $)
  $}

  $( ` { (/) } ` is the only topology with one element.  (Contributed by FL,
     18-Aug-2008.) $)
  top1 $p |- ( J e. Top -> ( J ~~ 1o <-> J = { (/) } ) ) $=
    ( ctop wcel c1o cen wbr c0 csn wceq wi 0opn en1eqsn ex syl 0ex ensn1 mpbiri
    breq1 impbid1 ) ABCZADEFZAGHZIZTGACZUAUCJAKUDUAUCGALMNUCUAUBDEFGOPAUBDERQS
    $.
    $( [1-Sep-2008] $) $( [18-Aug-2008] $)

  $( If a topology has two element it is the indiscrete topology.  (Contributed
     by FL, 11-Aug-2008.) $)
  top2ind $p |- ( ( J e. Top /\ J ~~ 2o ) -> J = { (/) , U. J } ) $=
    ( ctop wcel c2o cen wbr c0 cuni cpr wceq wi 0opn csn wb 0top wn c1o syl wne
    csdm eqid topopn 0ex ensn1 breq1 mpbiri 1sdom2 ensdomtr sdomnen mpan2 con2i
    wa bibif df-ne necom set2elt 3exp com24 com12 sylbi sylbir syl6bi com3r mpd
    com4l syl2im mpid pm2.43i imp ) ABCZADEFZAGAHZIJZVJVKVMKZVJVJGACZVNALVJVLGJ
    ZAGMZJZNZVJVLACZVOVNKAOAVLVLUAUBVKVSVTVOVMVKVRPZVSVTVOVMKKZKVRVKVRAQEFZVKPZ
    VRWCVQQEFGUCUDAVQQEUEUFWCQDTFZWDUGWCWEULADTFWDAQDUHADUIRUJRUKWAVSVKWBWAVSVP
    PZVKWBKZVPVRUMWFVLGSZWGVLGUNWHGVLSZWGVLGUOVKWIWBVKVOVTWIVMVKVOVTWIVMKGVLAUP
    UQURUSUTVAVBVCVDVEVFVGVHVI $.
    $( [1-Sep-2008] $) $( [11-Aug-2008] $)

  $( If a topology has two elements its underlying set can't be empty.
     (Contributed by FL, 11-Aug-2008.) $)
  top2usne $p |- ( ( J e. Top /\ J ~~ 2o ) -> U. J =/= (/) ) $=
    ( wcel c2o cen wbr wa c0 wceq wn wne wi wss breq1 csuc sssucid peano2 ax-mp
    com ensymg c1o ctop cuni cpr top2ind uni0b wo sssn sstr nsuceq0 necomi wpss
    csn df-pss peano1 php nsyl mpan sylbir mpan2 mp2an df-2o df-1o suceq breq2i
    syl eqtri mtbir pm2.21i syl6bi a1i com13 com12 0ex ensn1 2onn entr cvv 1onn
    adantl elisseti php5 syl6 a1d ex eqcoms mpi jaoi sylbi mpcom pm2.01d sylibr
    df-ne ) AUABZACDEZFZAUBZGHZIZWPGJWOWQAGWPUCHZWOWQWRKAUDWQWOWSWRWQAGULZLZWOW
    SWRKZKZAUEXAAGHZAWTHZUFXCAGUGXDXCXEWOXDXBWNXDXBKWMWSXDWNWRXDWNWRKKWSXDWNGCD
    EZWRAGCDMXFWRXFGGNZNZDEZGXGLZXGXHLZXIIZGOXGOXJXKFGXHLZXLGXGXHUHXMGXHJZXLXHG
    XGUIUJXMXNFGXHUKZXLGXHUMXHRBZXOXLXGRBZXPGRBXQUNGPQXGPQZXPXOFXHGDEZXIXHGUOXP
    XIXSKXRGXHRSQUPUQURUSVEUTCXHGDCTNZXHVATXGHXTXHHVBTXGVCQVFVDVGVHVIVJVKVSVLXE
    WTTDEZXCGVMVNYAXCKWTAWTAHYAATDEZXCWTATDMWOYBXBWNYBXBKZWMCRBZWNYCKVOYDWNCADE
    ZYCACRSYEYBXBYEYBFCTDEZXBCATVPYFWRWSTVQBZYFWRKTRVRVTYGYFTCDEZWRCTVQSYHWRYHT
    XTDEZTRBYIIVRTWAQCXTTDVAVDVGVHWBQWCVEWDWBQVSVLVIWEWFWGWHWHVKWIWJWPGWLWK $.
    $( [1-Sep-2008] $) $( [11-Aug-2008] $)

  $( An unnamed topological preservation theorem.  (Contributed by FL,
     18-Aug-2008.) $)
  homindlem2 $p |- ( ( K e. Top /\ K ~= { (/) } ) -> K = { (/) } ) $=
    ( ctop wcel c0 csn chomeo wbr wa c1o cen wceq wi sn0top homcard mpan2 ensn1
    imp 0ex jctir entr syl wb top1 adantr mpbid ) ABCZADEZFGZHZAIJGZAUGKZUIAUGJ
    GZUGIJGZHUJUIULUMUFUHULUFUGBCUHULLMAUGNOQDRPSAUGITUAUFUJUKUBUHAUCUDUE $.
    $( [1-Sep-2008] $) $( [18-Aug-2008] $)

  $( Homeomorphisms preserve topological indiscretion.  (Contributed by FL,
     18-Aug-2008.) $)
  homindlem3 $p |- ( ( K e. Top /\ A =/= (/) /\ A e. B ) -> ( { (/) , A } ~= K
     -> K = { (/) , U. K } ) ) $=
    ( ctop wcel c0 wne w3a cpr chomeo wbr c2o cen wa cuni wceq wi cvv syl ex
    simpl1 elisset cif preq2 eleq1d indistop dedth hmphsyma homcard ancoms syld
    com12 a1d 3imp1 0ex necom pr2nelem 3exp com13 mpii a1i 3imp adantr syl11anc
    sylbi entr jca top2ind syl6 ) CDEZAFGZABEZHZFAIZCJKZVJCLMKZNZCFCOIPVMVOVQVM
    VONZVJVPVJVKVLVOUAVRCVNMKZVNLMKZVPVJVKVLVOVSVJVLVOVSQZQVKVLVJWAVLVNDEZVJWAQ
    VLAREZWBABUBWCWBFWCAFUCZIZDEAFAWDPVNWEDAWDFUDUEWDUFUGSWBVJWAWBVJNVOCVNJKZVS
    VNCUHVJWBWFVSQCVNUIUJUKTSULUMUNVMVTVOVJVKVLVTVKVLVTQQVJVKVLFREZVTUOVKFAGZVL
    WGVTQQAFUPWGVLWHVTWGVLWHVTFARBUQURUSVEUTVAVBVCCVNLVFVDVGTCVHVI $.
    $( [1-Sep-2008] $) $( [18-Aug-2008] $)


