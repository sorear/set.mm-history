$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Planar incidence geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Plig $.

  $( Extend class notation with the class of all planar incidence geometries. $)
  cplig $a class Plig $.

  ${ $d x a b l c $.
    $( Planar incidence geometry. I use Hilbert's "axioms" adapted to
       planar geometry. ` e. ` is the incidence relation. I could take a
       generic incidence relation but I'm lazy and I'm not sure the
       gain is worth the extra work. Much of what follows is directly borrowed
       from Aitken. http://public.csusm.edu/aitken_html/m410/betweenness.pdf $)
    df-plig $a |- Plig = { x | ( A. a e. U. x  A. b e. U. x
          ( a =/= b -> E! l e. x ( a e. l /\ b e. l ) )
      /\ A. l e. x E. a e. U. x E. b e. U. x ( a =/= b /\ a e. l /\ b e. l )
      /\  E. a e. U. x E. b e. U. x E. c e. U. x A. l e. x
           -. ( a e. l /\ b e. l /\ c e. l ) ) } $.
  $}

  ${ $d L a b c l x $. $d P a b c x $.
   isplig.1 $e |- P = U. L $.
   $( The predicate " is a planar incidence geometry ". $)
   isplig $p |- ( L e. A -> ( L e. Plig <->
     ( A. a e. P A. b e. P ( a =/= b -> E! l e. L ( a e. l /\ b e. l ) )
     /\ A. l e. L E. a e. P E. b e. P ( a =/= b /\  a e. l /\ b e. l )
     /\ E. a e. P E. b e. P E. c e. P A. l e. L
           -. ( a e. l /\ b e. l /\ c e. l ) ) ) )  $=
     ( vx cv wne wel wa wreu wi cuni wral w3a wrex wn cplig wceq unieq syl6eqr
     reueq1 imbi2d raleq12d rexeq1d rexeq12d raleqd raleq1 3anbi123d df-plig
     elab2g ) DJEJKZDGLZEGLZMZGIJZNZOZEUSPZQZDVBQZUOUPUQRZEVBSZDVBSZGUSQZUPUQFG
     LRTZGUSQZFVBSZEVBSZDVBSZRUOURGCNZOZEBQZDBQZVEEBSZDBSZGCQZVIGCQZFBSZEBSZDBS
     ZRICUAAUSCUBZVDVQVHVTVMWDWEVCVPDVBBWEVBCPBUSCUCHUDZWEVAVOEVBBWFWEUTVNUOURG
     USCUEUFUGUGVGVSGUSCWEVFVRDVBBWFWEVEEVBBWFUHUIUJWEVLWCDVBBWFWEVKWBEVBBWFWEV
     JWAFVBBWFVIGUSCUKUIUIUIULIDEFGUMUN $.
     $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${ $d L a b l x $. $d P a b x $.
    efp.1 $e |- P = U. L $.
    $( Euclid's first postulate. There is a unique line passing through two
       distinct points. $)
    efp $p |- ( L e. Plig -> A. a e. P A. b e. P ( a =/= b
        -> E! l e. L ( a e. l /\ b e. l ) ) ) $=
      ( vx cplig wcel cv wne wel wa wreu wi wral w3a wrex wn isplig ibi
      3simp1d ) BHIZCJDJKZCELZDELZMEBNODAPCAPZUDUEUFQDARCAREBPZUEUFGELQSEBPGARD
      ARCARZUCUGUHUIQHABCDGEFTUAUB $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $}

  ${ $d A a b l $. $d B b l $. $d L a b l $. $d P a b $.
    efp2.1 $e |- P = U. L $.
    $( Euclid's first postulate. There is a unique line passing through two
       distinct points. $)
    efp2 $p |- ( ( L e. Plig /\ A e. P /\ B e. P ) -> ( A =/= B
        -> E! l e. L ( A e. l /\ B e. l ) ) ) $=
      ( va vb cplig wcel wne cv wa wreu wi wel wral efp wceq neeq1 eleq1
      anbi1d reubidv imbi12d neeq2 anbi2d rcla42v ex com3r syl 3imp ) DIJZACJZB
      CJZABKZAELZJZBUPJZMZEDNZOZULGLZHLZKZGEPZHEPZMZEDNZOZHCQGCQZUMUNVAOOCDGHEF
      RUMUNVJVAUMUNVJVAOVIVAAVCKZUQVFMZEDNZOGHABCCVBASZVDVKVHVMVBAVCTVNVGVLEDVN
      VEUQVFVBAUPUAUBUCUDVCBSZVKUOVMUTVCBAUEVOVLUSEDVOVFURUQVCBUPUAUFUCUDUGUHUI
      UJUK $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${ $d L a b l x $. $d P a b x $.
    l2p.1 $e |- P = U. L $.
    $( Any line has at least two distinct points. $)
    l2p $p |- ( L e. Plig -> A. l e. L E. a e. P E. b e. P
       ( a =/= b /\  a e. l /\ b e. l ) ) $=
      ( vx cplig wcel cv wne wel wa wreu wi wral w3a wrex wn isplig ibi
      3simp2d ) BHIZCJDJKZCELZDELZMEBNODAPCAPZUDUEUFQDARCAREBPZUEUFGELQSEBPGARD
      ARCARZUCUGUHUIQHABCDGEFTUAUB $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${ $d L a b c l $. $d P a b c $.
    tncp.1 $e |- P = U. L $.
    $( There exist three non colinear points. $)
    tncp $p |- ( L e. Plig -> E. a e. P E. b e. P E. c e. P A. l e. L
           -. ( a e. l /\ b e. l /\ c e. l ) ) $=
      ( cplig wcel cv wne wel wa wreu wi wral w3a wrex wn isplig ibi 3simp3d )
      BHIZCJDJKZCFLZDFLZMFBNODAPCAPZUDUEUFQDARCARFBPZUEUFEFLQSFBPEARDARCARZUCUG
      UHUIQHABCDEFGTUAUB $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

$( (End of FL's mathbox.) $)


