$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Planar incidence geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d L a b l x $.  $d P a b x $.
    efp.1 $e |- P = U. L $.
    $( Euclid's first postulate.  There is a unique line passing through two
       distinct points. $)
    efp $p |- ( L e. Plig -> A. a e. P A. b e. P ( a =/= b
        -> E! l e. L ( a e. l /\ b e. l ) ) ) $=
      ( vx cplig wcel cv wne wa wreu wi wral w3a wrex wn isplig ibi simp1d ) BH
      IZCJZDJZKZUCEJZIZUDUFIZLEBMNDAOCAOZUEUGUHPDAQCAQEBOZUGUHGJUFIPREBOGAQDAQC
      AQZUBUIUJUKPHABCDGEFSTUA $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $}

  ${
    $d A a b l $.  $d B b l $.  $d L a b l $.  $d P a b $.
    efp2.1 $e |- P = U. L $.
    $( Euclid's first postulate.  There is a unique line passing through two
       distinct points. $)
    efp2 $p |- ( ( L e. Plig /\ A e. P /\ B e. P ) -> ( A =/= B
        -> E! l e. L ( A e. l /\ B e. l ) ) ) $=
      ( va vb cplig wcel wne cv wa wreu wi wral efp wceq neeq1 eleq1 anbi1d
      reubidv imbi12d neeq2 anbi2d rcla42v ex com3r syl 3imp ) DIJZACJZBCJZABKZ
      AELZJZBUOJZMZEDNZOZUKGLZHLZKZVAUOJZVBUOJZMZEDNZOZHCPGCPZULUMUTOOCDGHEFQUL
      UMVIUTULUMVIUTOVHUTAVBKZUPVEMZEDNZOGHABCCVAARZVCVJVGVLVAAVBSVMVFVKEDVMVDU
      PVEVAAUOTUAUBUCVBBRZVJUNVLUSVBBAUDVNVKUREDVNVEUQUPVBBUOTUEUBUCUFUGUHUIUJ
      $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${
    $d L a b l x $.  $d P a b x $.
    l2p.1 $e |- P = U. L $.
    $( Any line has at least two distinct points. $)
    l2p $p |- ( L e. Plig -> A. l e. L E. a e. P E. b e. P
       ( a =/= b /\ a e. l /\ b e. l ) ) $=
      ( vx cplig wcel cv wne wa wreu wi wral w3a wrex wn isplig ibi simp2d ) BH
      IZCJZDJZKZUCEJZIZUDUFIZLEBMNDAOCAOZUEUGUHPDAQCAQEBOZUGUHGJUFIPREBOGAQDAQC
      AQZUBUIUJUKPHABCDGEFSTUA $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  $c line $.

  $( Extend class notation to include a function whose value is the line. $)
  cline $a class line $.

  ${
    $d x y p q l $.
    $( If ` x ` is a planar incidence geometry, and ` p ` and ` q ` are two
       points of the plane, ` ( ( line `` x ) `` <. p , q >. ) ` is the line
       passing through ` p ` and ` q ` . $)
    df-line $a |- line = { <. x , y >. | ( x e. Plig /\ y = { <. <. p ,
       q >. , l >. | ( ( p e. U. x /\ q e. U. x ) /\ ( p =/= q /\ ( l
       e. x /\ p e. l /\ q e. l ) ) ) } ) } $.
  $}

  ${
    $d L l p q x y $.  $d P l p q x y $.
    isline1.2 $e |- P = U. L $.
    $( The value of the function ` ( line `` L ) ` is the line passing through
       the points ` p ` and ` q ` . $)
    isline1 $p |- ( L e. Plig -> ( line ` L ) = { <. <. p , q >. , l
       >. | ( ( p e. P /\ q e. P ) /\ ( l e. L /\ ( p =/= q /\ p e. l
       /\ q e. l ) ) ) } ) $=
      ( vx vy cplig wcel cv wa wne w3a copab2 cvv cline cfv wceq wfun cdm wmo
      wal wi weu wreu efp2 imp 3anass eubii df-reu bitr4i sylibr eumo syl ex
      3expib simpr1 simpl simpr2 simpr3 3jca jca impbii mobii moanimv bitri
      syl6ibr 19.21aiv funoprabg cxp wss dmoprabss cuni uniexg syl5eqel xpexg
      syl11anc ssexg sylancr funex ax-17 unieq syl6eqr eleq2d biimpd anim12d
      eleq2 biimpcd 3ad2ant1 adantl impcom biimprd simprr1 biimprcd adantr
      simprr2 simprr3 impbid oprabbid df-line fvopab4g mpdan ) BIJZDKZAJZCKZAJZ
      LZEKZBJZXEXGMZXEXJJZXGXJJZNZLZLZDCEOZPJZBQRXRSXDXRTZXRUAZPJZXSXDXQEUBZCUC
      ZDUCXTXDYDDXDYCCXDXIXPEUBZUDYCXDXIXLXKXMXNNZEUBZUDZYEXDXFXHYHXDXFXHNZXLYG
      YIXLLZYFEUEZYGYJXMXNLZEBUFZYKYIXLYMXEXGABEFUGUHYKXKYLLZEUEYMYFYNEXKXMXNUI
      UJYLEBUKULUMYFEUNUOUPUQYEXLYFLZEUBYHXPYOEXPYOXPXLYFXKXLXMXNURXPXKXMXNXKXO
      USXKXLXMXNUTXKXLXMXNVAVBVCYOXKXOXLXKXMXNURYOXLXMXNXLYFUSXLXKXMXNUTXLXKXMX
      NVAVBVCVDVEXLYFEVFVGVHXIXPEVFUMVIVIXQDCEVJUOXDYAAAVKZVLYPPJZYBXPDCEAAVMXD
      APJZYRYQXDBVNZPABIVOFVPZYTAAPPVQVRYAYPPVSVTPXRWAVRGHBXEGKZVNZJZXGUUBJZLZX
      LXJUUAJZXMXNNZLZLZDCEOXRIPQUUABSZUUIXQDCEUUJDWBUUJCWBUUJEWBUUJUUIXQUUJUUE
      XIUUHXPUUJUUCXFUUDXHUUJUUCXFUUJUUBAXEUUJUUBYSAUUABWCFWDZWEZWFUUJUUDXHUUJU
      UBAXGUUKWEZWFWGUUJUUHXPUUJUUHLXKXOUUHUUJXKUUGUUJXKUDZXLUUFXMUUNXNUUJUUFXK
      UUABXJWHZWIWJWKWLUUHXOUUJUUHXLXMXNXLUUGUSXLUUFXMXNUTXLUUFXMXNVAVBWKVCUPWG
      UUJXIUUEXPUUHUUJXFUUCXHUUDUUJUUCXFUULWMUUJUUDXHUUMWMWGUUJXPUUHUUJXPLZXLUU
      GXLXMXNXKUUJWNUUPUUFXMXNXPUUJUUFXKUUJUUFUDXOUUJUUFXKUUOWOWPWLXLXMXNXKUUJW
      QXLXMXNXKUUJWRVBVCUPWGWSWTGHCDEXAXBXC $.
      $( [24-Apr-2011] $)
  $}

