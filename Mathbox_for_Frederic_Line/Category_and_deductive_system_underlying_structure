$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Category and deductive system underlying "structure"
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Alg $. $( The class of all the "structures" used by ` Ded `
               and ` Cat ` . $)

  $c dom_ $. $( Function returning the function domain of a category. Despite
  its name the domain of a category is not the same concept as the domain of
  a relation. $)

  $c cod_ $. $( Function returning the function codomain of a category. $)

  $c id_ $. $( Function returning the function identity of a category. $)

  $c o_ $. $( Function returning the composition of morphisms of a category. $)


  $( Extend class notation with the class of structures used by ` Cat ` and
     ` Ded ` . $)
  calg $a class Alg $.

  $( Extend class notation with the function returning the function domain of a
     category. $)
  cdom_ $a class dom_ $.

  $( Extend class notation with the function returning the function codomain of
     a category. $)
  ccod_ $a class cod_ $.

  $( Extend class notation with the function returning the function identity of
     a category. $)
  cid_ $a class id_ $.

  $( Extend class notation with the function returning the composition of
     morphisms of a category. $)
  co_ $a class o_ $.

  ${
    $d x d c j r $.
    $( ` Ded ` and ` Cat ` structure.  Metamath for internal reasons doesn't
       like too large definitions.  Then ` Cat ` has been split giving birth to
       ` Ded ` and ` Alg ` .  If ` Ded ` has a real mathematical use, ` Alg `
       is only here to give relief to Metamath. $)
    df-alg $a |- Alg = { x | E. d E. c E. j E. r
      ( x = <. <. d , c >. , <. j , r >. >. /\
      ( d : dom d --> dom j /\ c : dom d --> dom j /\ j : dom j --> dom d ) /\
      ( Fun r /\ dom r C_ ( dom d X. dom d ) /\ ran r C_ dom d ) ) } $.
  $}

  $( Definition of ` dom_ ` . $)
  df-doma $a |- dom_ = ( 1st o. 1st ) $.

  $( Definition of ` cod_ ` . $)
  df-coda $a |- cod_ = ( 2nd o. 1st ) $.

  $( Definition of ` id_ ` . $)
  df-ida $a |- id_ = ( 1st o. 2nd ) $.

  $( Definition of ` o_ ` . $)
  df-cmpa $a |- o_ = ( 2nd o. 2nd ) $.

  ${
    $d C c d j r $.  $d D c d j r $.  $d J c d j r $.  $d M c d j r $.
    $d O c d j r $.  $d R c d j r $.  $d c d j r x $.
    isalg.1 $e |- M = dom D $.
    isalg.2 $e |- O = dom J $.
    $( The predicate "has the structure required by ` Ded ` and ` Cat ` ." $)
    isalg $p |- ( ( ( D e. A /\ C e. B /\ J e. F ) /\ R e. G )
                -> ( <. <. D , C >. , <. J , R >. >. e. Alg
                  <-> ( ( D : M --> O /\ C : M --> O /\ J : O --> M )
                    /\ ( Fun R /\ dom R C_ ( M X. M ) /\ ran R C_ M ) ) ) ) $=
      ( vx vd vc vj vr wcel w3a wa cop cv wceq cdm wf wfun cxp wss crn wex cab
      calg feq1 wb dmeq eqcomi eqeq2i biimpi feq2 3syl bitrd feq3 3anbi123d
      syl6eqr xpid11 biimpri sseq2 3anbi23d anbi12d 3anbi2d anbi1d funeq
      sseq1d rneq anbi2d elo 3anass exbii 3exbii abbii eleq2i syl5bb df-alg )
      DARCBRHFRSEGRTZDCUAHEUAUAZMUBNUBZOUBZUAPUBZQUBZUAUAUCZWFUDZWHUDZWFUEZWKWL
      WGUEZWLWKWHUEZSZWIUFZWIUDZWKWKUGZUHZWIUIZWKUHZSZSZQUJZPUJOUJNUJZMUKZRZIJD
      UEZIJCUEZJIHUEZSZEUFZEUDZIIUGZUHZEUIZIUHZSZTZWEULRWDWEWJWPXCTZTZQUJZPUJOU
      JNUJZMUKZRXTXHYAIWLDUEZIWLWGUEZWLIWHUEZSZWQWRXOUHZXAIUHZSZTYFIWLCUEZYHSZY
      LTXLYLTXTMNOQPDCHEABFGWFDUCZWPYIXCYLYOWMYFWNYGWOYHYOWMWKWLDUEZYFWKWLWFDUM
      YOWKDUDZUCZWKIUCZYPYFUNWFDUOZYRYSYQIWKIYQKUPUQURZWKIWLDUSUTVAYOYRYSWNYGUN
      YTUUAWKIWLWGUSUTYOYRYSWOYHUNYTUUAWKIWLWHVBUTVCYOWTYJXBYKWQYOYSWSXOUCZWTYJ
      UNYOWKYQIYTKVDUUBYSWKIVEVFWSXOWRVGUTYOYRYSXBYKUNYTUUAWKIXAVGUTVHVIWGCUCZY
      IYNYLUUCYGYMYFYHIWLWGCUMVJVKWHHUCZYNXLYLUUDYFXIYMXJYHXKUUDWLHUDZUCZWLJUCZ
      YFXIUNWHHUOZUUFUUGUUEJWLJUUELUPUQURZWLJIDVBUTUUDUUFUUGYMXJUNUUHUUIWLJICVB
      UTUUDYHWLIHUEZXKWLIWHHUMUUDUUFUUGUUJXKUNUUHUUIWLJIHUSUTVAVCVKWIEUCZYLXSXL
      UUKWQXMYJXPYKXRWIEVLUUKWRXNXOWIEUOVMUUKXAXQIWIEVNVMVCVOVPXGYEWEXFYDMXEYCN
      OPXDYBQWJWPXCVQVRVSVTWAWBULXGWEMPQONWCWAWB $.
      $( [28-Oct-2007] $) $( [24-Oct-2007] $)
  $}

  ${
    1alg.1 $e |- A e. _V $.
    $( Category ` 1 ` has the structure required by ` Ded ` and ` Cat ` . $)
    1alg $p |- <. <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ,
      <. { <. A , <. A , A >. >. } , { <. <. <. A , A >. , <. A , A >. >. ,
      <. A , A >. >. } >. >. e. Alg $=
      ( cop csn calg wcel wf w3a wfun cdm cxp wss crn cvv wa wb snex 3pm3.2i
      dmsnop eqcomi isalg mp2an wf1o opex f1osn f1of ax-mp funsn fssxp eqsstri
      rnsnop eqimssi mpbir2an ) AACZACZDZUPCAUNCZDZUNUNCZUNCZDZCCEFZUNDZADZUPGZ
      VEVDVCURGZHZVAIZVAJZVCVCKZLZVAMZVCLZHZUPNFZVOURNFZHVANFVBVGVNOPVOVOVPUOQZ
      VQUQQRUTQNNUPUPVANNURVCVDUPJVCUNASTURJVDAUNSTUAUBVEVEVFVCVDUPUCVEUNAAAUDZ
      BUEVCVDUPUFUGZVSVDVCURUCVFAUNBVRUEVDVCURUFUGRVHVKVMUSUNUNUNUDZVRUHVIUSDZV
      JUSUNSVCVCWAGZWAVJLVCVCWAUCWBUNUNVRVRUEVCVCWAUFUGVCVCWAUIUGUJVLVCUSUNVTVR
      UKULRUM $.
      $( [31-Oct-2007] $) $( [30-Oct-2007] $)
  $}

  ${
    domval.1 $e |- D = ( dom_ ` T ) $.
    $( Value of the domain function expressed with the ` 1st ` function. $)
    domval $p |- D = ( 1st ` ( 1st ` T ) ) $=
      ( cdom_ cfv c1st cvv wcel wceq ccom wfun wf wfo fo1st fofun ax-mp fof
      fvco3 mp3an12 df-doma fveq1i syl5eq wn c0 fvprc fveq2d 1st0 syl6req
      eqtrd pm2.61i eqtri ) ABDEZBFEZFEZCBGHZULUNIUOBFFJZEZUNULFKZGGFLZUOUQUNIG
      GFMZURNGGFOPUTUSNGGFQPGGBFFRSBDUPTUAUBUOUCZULUDUNBDUEVAUNUDFEUDVAUMUDFBFU
      EUFUGUHUIUJUK $.
      $( [28-Oct-2007] $) $( [24-Oct-2007] $)
  $}

  ${
    codval.1 $e |- C = ( cod_ ` T ) $.
    $( Value of the function codomain expressed with the ` 1st ` and ` 2nd `
       functions. $)
    codval $p |- C = ( 2nd ` ( 1st ` T ) ) $=
      ( ccod_ cfv c1st c2nd cvv wcel wceq ccom wfun wf wfo fo2nd fofun ax-mp
      fo1st fof fvco3 mp3an12 df-coda fveq1i syl5eq wn c0 fvprc fveq2d 2nd0
      syl6req eqtrd pm2.61i eqtri ) ABDEZBFEZGEZCBHIZUNUPJUQBGFKZEZUPUNGLZHHFMZ
      UQUSUPJHHGNUTOHHGPQHHFNVARHHFSQHHBGFTUABDURUBUCUDUQUEZUNUFUPBDUGVBUPUFGEU
      FVBUOUFGBFUGUHUIUJUKULUM $.
      $( [28-Oct-2007] $) $( [26-Oct-2007] $)
  $}

  ${
    idval.1 $e |- J = ( id_ ` T ) $.
    $( Value of the identity function expressed with the ` 1st ` and ` 2nd `
       functions. $)
    idval $p |- J = ( 1st ` ( 2nd ` T ) ) $=
      ( cid_ cfv c2nd c1st cvv wcel wceq ccom wfun wf wfo fo1st fofun ax-mp
      fo2nd fof fvco3 mp3an12 df-ida fveq1i syl5eq wn c0 fvprc fveq2d 1st0
      syl6req eqtrd pm2.61i eqtri ) BADEZAFEZGEZCAHIZUNUPJUQAGFKZEZUPUNGLZHHFMZ
      UQUSUPJHHGNUTOHHGPQHHFNVARHHFSQHHAGFTUAADURUBUCUDUQUEZUNUFUPADUGVBUPUFGEU
      FVBUOUFGAFUGUHUIUJUKULUM $.
      $( [28-Oct-2007] $) $( [26-Oct-2007] $)
  $}

  ${
    cmpval.1 $e |- G = ( o_ ` T ) $.
    $( Value of the identity function expressed with the ` 2nd ` functions. $)
    cmpval $p |- G = ( 2nd ` ( 2nd ` T ) ) $=
      ( co_ cfv c2nd cvv wcel wceq ccom wfun wf wfo fo2nd fofun ax-mp fof
      fvco3 mp3an12 df-cmpa fveq1i syl5eq wn c0 fvprc fveq2d 2nd0 syl6req
      eqtrd pm2.61i eqtri ) BADEZAFEZFEZCAGHZULUNIUOAFFJZEZUNULFKZGGFLZUOUQUNIG
      GFMZURNGGFOPUTUSNGGFQPGGAFFRSADUPTUAUBUOUCZULUDUNADUEVAUNUDFEUDVAUMUDFAFU
      EUFUGUHUIUJUK $.
      $( [28-Oct-2007] $) $( [26-Oct-2007] $)
  $}

  ${
    $d C c d j r $.  $d D c d j r $.  $d J c d j r $.  $d M c d j r $.
    $d O c d j r $.  $d R c d j r $.  $d T c d j r x $.
    algi.1 $e |- D = ( dom_ ` T ) $.
    algi.2 $e |- C = ( cod_ ` T ) $.
    algi.3 $e |- J = ( id_ ` T ) $.
    algi.4 $e |- R = ( o_ ` T ) $.
    algi.5 $e |- M = dom D $.
    algi.6 $e |- O = dom J $.
    $( "Axiomatic" properties of ` Alg ` . $)
    algi $p |- ( T e. Alg
         -> ( ( D : M --> O /\ C : M --> O /\ J : O --> M )
               /\ ( Fun R /\ dom R C_ ( M X. M ) /\ ran R C_ M ) ) ) $=
      ( vx vd vc vj vr calg wcel cv cop wceq cdm wf w3a wfun cxp wss crn wex
      cab wa df-alg eleq2i 3anass bicomi exbii 3exbii abbii c1st cfv wb domval
      eqcomi eqeq2i feq1 dmeq feq2d feq2 ax-mp syl6bbr bitrd syl biimpi feq3
      3syl 3anbi123d xpid11 sseq2 sylbir 3anbi23d anbi12d sylbi c2nd codval
      3anbi2d anbi1d idval cmpval funeq sseq1d rneq anbi2d eloi ) DSTDNUAOUAZPU
      AZUBQUAZRUAZUBUBUCZWPUDZWRUDZWPUEZXAXBWQUEZXBXAWRUEZUFZWSUGZWSUDZXAXAUHZU
      IZWSUJZXAUIZUFZUFZRUKZQUKPUKOUKZNULZTZFGBUEZFGAUEZGFEUEZUFZCUGZCUDZFFUHZU
      IZCUJZFUIZUFZUMZSXQDNQRPOUNUOXRDWTXFXMUMZUMZRUKZQUKPUKOUKZNULZTYJYOXQDYNX
      PNYMXOOPQYLXNRXNYLWTXFXMUPUQURUSUTUOYKFXBBUEZFXBWQUEZXBFWRUEZUFZXGXHYEUIZ
      XKFUIZUFZUMZYPFXBAUEZYRUFZUUBUMZYBUUBUMZYJNOPRQDWPDVAVBZVAVBZUCWPBUCZYKUU
      CVCUUIBWPBUUIBDHVDVEVFUUJXFYSXMUUBUUJXCYPXDYQXEYRUUJXCXAXBBUEZYPXAXBWPBVG
      UUJUUKBUDZXBBUEZYPUUJXAUULXBBWPBVHZVIFUULUCZYPUUMVCLFUULXBBVJVKVLVMUUJXAU
      ULUCZXDYQVCUUNUUPXDUULXBWQUEZYQXAUULXBWQVJUUOYQUUQVCLFUULXBWQVJVKVLVNUUJU
      UPXAFUCZXEYRVCUUNUUPUURUULFXAFUULLVEVFVOZXAFXBWRVPVQVRUUJUUPUURXMUUBVCUUN
      UUSUURXJYTXLUUAXGUURXIYEUCXJYTVCXAFVSXIYEXHVTWAXAFXKVTWBVQWCWDWQUUHWEVBZU
      CWQAUCZUUCUUFVCUUTAWQAUUTADIWFVEVFUVAYSUUEUUBUVAYQUUDYPYRFXBWQAVGWGWHWDWR
      DWEVBZVAVBZUCWREUCZUUFUUGVCUVCEWREUVCDEJWIVEVFUVDUUEYBUUBUVDYPXSUUDXTYRYA
      UVDXBEUDZUCZXBGUCZYPXSVCWREVHZUVFUVGUVEGXBGUVEMVEVFVOZXBGFBVPVQUVDUVFUVGU
      UDXTVCUVHUVIXBGFAVPVQUVDYRXBFEUEZYAXBFWREVGUVDUVFUVGUVJYAVCUVHUVIXBGFEVJV
      QVMVRWHWDWSUVBWEVBZUCWSCUCZUUGYJVCUVKCWSCUVKDCKWJVEVFUVLUUBYIYBUVLXGYCYTY
      FUUAYHWSCWKUVLXHYDYEWSCVHWLUVLXKYGFWSCWMWLVRWNWDWOWAWD $.
      $( [28-Oct-2007] $) $( [24-Oct-2007] $)
  $}

  ${

    alg.1 $e |- M = dom D $.
    alg.3 $e |- D = ( dom_ ` T ) $.
    ${
      alg.2 $e |- O = dom J $.
      alg.5 $e |- J = ( id_ ` T ) $.

      ${
        $( ` ( dom_ `` T ) ` is a mapping from the morphisms of ` T ` to the
           objects of ` T ` . $)
        doma $p |- ( T e. Alg -> D : M --> O ) $=
          ( calg wcel wf ccod_ cfv w3a co_ wfun cdm cxp wss crn eqid algi
          pm3.26d 3simp1d ) BJKZDEALZDEBMNZLZEDCLZUFUGUIUJOBPNZQUKRDDSTUKUADTOU
          HAUKBCDEGUHUBIUKUBFHUCUDUE $.
          $( [28-Oct-2007] $) $( [24-Oct-2007] $)
      $}

      ${
        alg.4 $e |- C = ( cod_ ` T ) $.
        $( ` ( cod_ `` T ) ` is a mapping from the morphisms of ` T ` to the
           objects of ` T ` . $)
        coda $p |- ( T e. Alg -> C : M --> O ) $=
          ( calg wcel wf w3a co_ cfv wfun cdm cxp wss crn eqid algi pm3.26d
          3simp2d ) CLMZEFBNZEFANZFEDNZUGUHUIUJOCPQZRUKSEETUAUKUBEUAOABUKCDEFHK
          JUKUCGIUDUEUF $.
          $( [28-Oct-2007] $) $( [26-Oct-2007] $)

      $}

      ${
        $( ` ( id_ `` A ) ` is a mapping from the objects of ` T ` to the
           morphisms of ` T ` . $)
        ida $p |- ( T e. Alg -> J : O --> M ) $=
          ( calg wcel wf ccod_ cfv w3a co_ wfun cdm cxp wss crn eqid algi
          pm3.26d 3simp3d ) BJKZDEALZDEBMNZLZEDCLZUFUGUIUJOBPNZQUKRDDSTUKUADTOU
          HAUKBCDEGUHUBIUKUBFHUCUDUE $.
          $( [28-Oct-2007] $) $( [26-Oct-2007] $)

        $( An identity is a morphism. $)
        idmoa $p |- ( ( T e. Alg /\ A e. O ) -> ( J ` A ) e. M ) $=
          ( wf wcel cfv calg ffvelrn ida sylan ) FEDKAFLADMELCNLFEADOBCDEFGHIJP
          Q $.
          $( [10-Jan-2008] $) $( [10-Jan-2008] $)
      $}
    $}

    ${
      alg.6 $e |- R = ( o_ ` T ) $.
      $( ` ( o_ `` T ) ` is a partial operation on the morphisms of ` T ` . $)
      cmppfa $p |- ( T e. Alg ->
        ( Fun R /\ dom R C_ ( M X. M ) /\ ran R C_ M ) ) $=
        ( calg wcel cid_ cfv cdm wf ccod_ w3a wfun cxp wss crn eqid algi
        pm3.27d ) CHIDCJKZLZAMDUDCNKZMUDDUCMOBPBLDDQRBSDROUEABCUCDUDFUETUCTGEUD
        TUAUB $.
        $( [28-Oct-2007] $) $( [26-Oct-2007] $)
    $}
  $}

  ${
    dcsda.1 $e |- D = ( dom_ ` T ) $.
    dcsda.2 $e |- C = ( cod_ ` T ) $.
    $( ` ( dom_ `` T ) ` and ` ( cod_ `` T ) ` have the same domain. $)
    dcsda $p |- ( T e. Alg -> dom D = dom C ) $=
      ( calg wcel cdm cid_ cfv wf wceq eqid coda fdm eqcomd syl ) CFGBHZCIJZHZA
      KZRAHZLABCSRTRMDTMSMENUAUBRRTAOPQ $.
      $( [10-Jan-2008] $) $( [10-Jan-2008] $)
  $}

