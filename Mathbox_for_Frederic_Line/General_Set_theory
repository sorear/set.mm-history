$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              General Set theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y z $.  $d B x y z $.
    $( The intersection of a union ` U. A ` with a class ` B ` is equal to the
       union of the intersections of each element of ` A ` with ` B ` . $)
    ntunte $p |- ( U. A i^i B ) = U. { x | E. y e. A x = ( y i^i B ) } $=
      ( vz cuni cin cv wceq wrex cab wcel wa wex eluni2 anbi1i elin ancom
      r19.41v bitr4i exbii rexcom4 visset inex1 eleq2 ceqsexv bitri rexbii
      3bitri 3bitr4i eluniab eqriv ) ECFZDGZAHZBHZDGZIZBCJZAKFZEHZUNLZVAUOLZUSM
      ZANZVAUTLVAUMLZVADLZMVAUPLZBCJZVGMZVBVEVFVIVGBVACOPVAUMDQVEURVCMZANZBCJZV
      HVGMZBCJVJVEVKBCJZANVMVDVOAVDUSVCMVOVCUSRURVCBCSTUAVKBACUBTVLVNBCVLVAUQLZ
      VNVCVPAUQUPDBUCUDUOUQVAUEUFVAUPDQUGUHVHVGBCSUIUJUSAVAUKTUL $.
      $( [24-Mar-2007] $) $( [24-Mar-2007] $)
  $}


  ${
    $d a b c x u v $.  $d a b x C $.  $d a b c x B $.  $d a b x u v A $.
    $d R a b u v x $.  $d C a b c x $.
    uninqs.1 $e |- Er R $.
    $( Class union distributes over the intersection of two subclasses of a
       quotient space.  Compare ~ uniin . $)
    uninqs  $p |- ( ( B (_ ( A /. R ) /\ C (_ ( A /. R ) ) ->
      U. ( B i^i C ) = ( U. B i^i U. C ) ) $=
      ( vx vc vb va vu vv cqs wss wa cin cuni wceq cv wcel wex wi wal dfss2
      pm3.35 cec wrex visset elqs c0 wne inelcm wn df-ne wo erdisj pm5.61
      eqeq12 biimprcd adantr sylbi mpan syl eleq2 biimpa syl2an an4s exp32
      com24 pm2.43i ex a1i r19.23aiv com3l com23 imp com13 imp31 eleq1 elin
      elequ2 biimprd wb incom eleq2i syl6bb equcoms biimpd anim12d a4imev
      expcom sylbir syl6bi syl9 com3r pm2.43a com14 com4t a4s com12 19.23aiv
      eluni syl7ib syl5ib imp3a 3imtr4g ssrdv uniin jctil eqss sylibr ) BADLZMZ
      CXKMZNZBCOZPZBPZCPZOZMZXSXPMZNXPXSQXNYAXTXNFXSXPXNFRZXQSZYBXRSZNYBGRZSZYE
      XOSZNZGTZYBXSSYBXPSXNYCYDYIXNYBHRZSZYJBSZNZHTZYDYIUAYCXNYNYBIRZSZYOCSZNZI
      TZYIYDYNXNYSYIUAZYMXNYTUAHYSXNYMYIYRXNYMYIUAZUAIXNYRUUAXLXMYRUUAUAZXLYLYJ
      XKSZUAZHUBXMUUBUAZHBXKUCUUDUUEHXMUUDUUBXMYQYOXKSZUAZIUBUUDUUBUAZICXKUCUUG
      UUHIYRYMUUGUUDYIYPYQYMUUGUUDYIUAZUAZUAYMYQYPUUJYKYLYQYPUUJUAZUAZYKYLUULUA
      YLYKYKUULYKYLYKUULUAZYLYKYLUUMUAYQYLYKYLYKNZUUKYLYQYKUUNUUKUAZUAZYQYLYQUU
      PUAYQYLNZYKYQUUOUUQYKYQUUOUAYQUUNUUQYKNZUUKYQUUNUURUUKUAYQUUNNZYPUURUUJUU
      SYPUURUUJUAUUSYPNZUUGUURUUIUUTUUGUURUUIUAUUTUUGNUUDYOYJQZUURYIUUSYPUUGUUD
      UVAUAUUSUUDUUGYPUVAYQUUNUUDUUGYPUVAUAZUAUAYQUUGUUDUUNUVBYQUUGUUDUUNUVBUAU
      AZYQUUGNUUFUVCYQUUFUDUUNUUDUUFUVBYLYKUUDUUFUVBUAZUAYLUUDYKUVDYLUUDYKUVDUA
      ZYLUUDNUUCUVEYLUUCUDUUCYJJRZDUEZQZJAUFUVEJAYJDHUGUHUVHUVEJAUVHUVEUAUVFASU
      UFUVHYKUVBUUFYOKRZDUEZQZKAUFUVHYKUVBUAZUAZKAYODIUGUHUVKUVMKAUVKUVMUAUVIAS
      UVKUVHUVLUVKUVHNZUVLUVNYPYKUVNUVAUVNYPYKUVNUVAUAZUVKYPUVHYKUVOYBUVJSZYBUV
      GSZUVOUVKYPNUVHYKNUVPUVQNUVJUVGOZUIUJZUVOYBUVJUVGUKUVSUVRUIQZULZUVOUVRUIU
      MUVJUVGQZUVTUNZUWAUVOUVIUVFDKUGJUGEUOUWCUWANUWBUWANUVOUWBUVTUPUWBUVOUWAUV
      NUVAUWBYOUVJYJUVGUQURUSUTVAUTVBUVKYPUVPYOUVJYBVCVDUVHYKUVQYJUVGYBVCVDVEVF
      VGVHVIVJVKVLUTVMVKVLUTVBVJVNVOVPVBVJVHVOVHVQUUQYKUVAYIUAUUQUVAYKYIYQYLUVA
      YKYIUAZUAUVAYQYLUWDUVAYQYJCSZYLUWDUAYOYJCVRUWEYLUWDUWEYLNYJCBOZSZUWDYJCBV
      SYKUWGYIYKUWGNYHGHYEYJQZYKYFUWGYGUWHYFYKGHFVTWAUWHUWGYGUWGYGWBHGYJYEQUWGY
      EUWFSYGYJYEUWFVRUWFXOYECBWCWDWEWFWGWHWIWJWKVJWLVMVOVNVOWMVJVNVJVNVJWNVJVN
      VJWOWPVJWOVPVIVOVPVOWQWRUTWSWRUTVOWSWTVPWTWSIYBCXAXBHYBBXAXCXDYBXQXRVSGYB
      XOXAXEXFBCXGXHXPXSXIXJ $.
      $( [4-Jun-2007] $) $( [25-May-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.
    erdisj2.1 $e |- A e. V $.
    erdisj2.2 $e |- B e. V $.
    $( Equivalence classes do not overlap. $)
    erdisj2 $p |- ( Er R -> ( [ A ] R = [ B ] R \/
      ( [ A ] R i^i [ B ] R ) = (/) ) ) $=
      ( wer cec wceq cin c0 wo cid cif eceq1 eqeq12d ineq12d eqeq1d orbi12d
      ereq ider elimhyp erdisj dedth ) CFZACGZBCGZHZUEUFIZJHZKAUDCLMZGZBUJGZHZU
      KULIZJHZKCLCUJHZUGUMUIUOUPUEUKUFULCUJANZCUJBNZOUPUHUNJUPUEUKUFULUQURPQRAB
      UJDEUDUJFLFCLCUJSLUJSTUAUBUC $.
      $( [4-Jun-2007] $) $( [1-Jun-2007] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Inference from membership to difference. $)
    difeqri2 $p |- ( A. x ( ( x e. A /\ -. x e. B ) <-> x e. C )
      -> ( A \ B ) = C ) $=
      ( cv wcel wn wa wb wal cab cdif wceq bicom albii biimpi abeq2 sylibr
      df-dif syl6reqr ) AEZBFUACFGHZUADFZIZAJZDUBAKZBCLUEUCUBIZAJZDUFMUEUHUDUGA
      UBUCNOPUBADQRABCST $.
      $( [18-Jun-2007] $) $( [6-Jun-2007] $)
  $}

  ${ $d A u v w y z $.  $d B u v w y z $.  $d C u v w y z $.  $d D u v w y z $.
     $d ph u x $.  $d ta u v w y z $.  $d v w x y z $.
    elo.1 $e |-   ( y = A -> ( ph <-> ps ) ) $.
    elo.2 $e |-   ( z = B -> ( ps <-> ch ) ) $.
    elo.3 $e |-   ( v = C -> ( ch <-> th ) ) $.
    elo.4 $e |-   ( w = D -> ( th <-> ta ) ) $.
    $( The law of concretion for operation class abstraction.
    Compare with  ~ eloprabg . This version is to be used with categories. $)
    elo $p |- ( ( ( A e. Q /\ B e. R /\ C e. S ) /\ D e. T ) ->
      ( <. <. A , B >. , <. C , D >. >. e. { x  | E. y E. z E. v E. w
      ( x = <. <. y , z >. , <. v , w >. >. /\ ph ) } <-> ta ) ) $=
      ( vu cvv wcel w3a cop cv wceq wa wex cab wb wi opex eqeq1 eqcom syl6bb
      cxp visset otthg 3adant3 opthg 3anbi3d and4as 3ad2ant3 bitrd 3simp2
      adantr opelxpi 3ad2antl3 pm3.27 syl3anc sylan9bbr anbi1d syl3an9b
      sylan9bb pm5.32i 4exbidv eleq1 elab syl5bbr adantl elex 3anim123i
      anim12i eeeeanv sylibr biantrurd 19.41vvvv syl6rbbr 3bitr3d expcom
      vtocle elisset syl2an ) KUDUEZLUDUEZMUDUEZUFZNUDUEZKLUGZMNUGZUGZFUHZGUHZH
      UHZUGJUHZIUHZUGZUGZUIZAUJZIUKJUKHUKGUKZFULZUEZEUMZKOUEZLPUEZMQUEZUFNRUEWT
      XAUJZXQUNUCXDXBXCUOYAUCUHZXDUIZXQYAYCUJZYBXKUIZAUJZIUKJUKHUKGUKZXFKUIZXGL
      UIZXHMUIZUFZXINUIZUJZEUJZIUKJUKHUKGUKZXPEYDYFYNGHJIYDYFYMAUJYNYDYEYMAYCYE
      XKXDUIZYAYMYCYEXDXKUIYPYBXDXKUPXDXKUQURWRXCUDUDUSZUEZXAYPYMUMYAWRYRXAUFYP
      YHYIXJXCUIZUFZYMWRYRYPYTUMXAXFXGKLXJXCUDYQGUTHUTXHXIUOVAVBXAWRYTYMUMYRXAY
      TYHYIYJYLUJZUFYMXAYSUUAYHYIXHXIMNUDJUTIUTVCVDYHYIYJYLVEURVFVGWTWRXAWQWRWS
      VHVIWSWQXAYRWRMNUDUDVJVKWTXAVLVMVNVOYMAEYKADYLEYHABYICYJDSTUAVPUBVQVRURVS
      YCYGXPUMYAYCYBXOUEXPYGYBXDXOVTXNYGFYBUCUTXEYBUIZXMYFGHJIUUBXLYEAXEYBXKUPV
      OVSWAWBWCYAYOEUMYCYAEYMIUKJUKHUKGUKZEUJYOYAUUCEYAYHGUKZYIHUKZYJJUKZUFZYLI
      UKZUJUUCWTUUGXAUUHWQUUDWRUUEWSUUFGKUDWDHLUDWDJMUDWDWEINUDWDWFYHYIYJYLHJIG
      WGWHWIYMEHJIGWJWKVIWLWMWNXRWQXSWRXTWSKOWOLPWOMQWOWENRWOWP $.
      $( [14-Jul-2007] $) $( [14-Jul-2007] $)
  $}

  ${
    $d A x y $.  $d C x $.
    $( Specific properties of a finite intersection. $)
    spfi $p |- ( A e. B -> ( A e. { x | E. y ( y (_ C /\ y e. Fin
         /\ x = |^| y ) } <-> E. y ( y (_ C /\ y e. Fin /\ A = |^| y ) ) ) $=
      ( cv wss cfn wcel cint wceq w3a wex eqeq1 3anbi3d exbidv elabg ) BFZEGZRH
      IZAFZRJZKZLZBMSTCUBKZLZBMACDUACKZUDUFBUGUCUESTUACUBNOPQ $.
      $( [2-Sep-2008] $) $( [30-Oct-2007] $)
  $}

  ${
    $d a b x y A $.  $d a b x y B $.  $d a b x y C $.
    $( The intersection of two finite intersections is finite. $)
    infi1 $p |- ( ( A e. { x | E. y ( y (_ C /\ y e. Fin /\ x = |^| y ) }
                    /\ B e. { x | E. y ( y (_ C /\ y e. Fin /\ x = |^| y ) } )
        -> ( A i^i B ) e. { x | E. y ( y (_ C /\ y e. Fin /\ x = |^| y ) } ) $=
      ( va vb cv wss cfn wcel cint wceq w3a wex cab wa cin cvv inex1g adantr
      wi spfi cun sseq1 eleq1 inteq eqeq2d 3anbi123d cla4egv visset unex a1i
      an6 unss biimpi unfi ineq2 ineq1 adantl incom intun eqtr4i 3eqtrd
      3anim123i sylbi sylc expcom 19.23aiv com12 imp cbvexv syl2anb 3adant3 wb
      bicomd 3ad2ant3 mpbid 3exp syl6bi imp31 an4s ex mpid pm2.43i ) CBHZEIZWFJ
      KZAHWFLZMNBOAPZKZDWJKZQZCDRZWJKZWMWMWNSKZWOWKWPWLCDWJTUAWMWMWPWOUBZWKWKWL
      WLWQWKWKWLWLQZWQWKWKWGWHCWIMZNZBOZWRWQUBABCWJEUCWRXAWQWLWLXAWQUBZWLWLWGWH
      DWIMZNZBOZXBABDWJEUCXEXAWPWOXEXAWPNWGWHWNWIMZNZBOZWOXEXAXHWPFHZEIZXIJKZDX
      ILZMZNZFOZGHZEIZXPJKZCXPLZMZNZGOZXHXEXAXOYBXHXNYBXHUBFYBXNXHYAXNXHUBGXNYA
      XHXIXPUDZSKZYCEIZYCJKZWNYCLZMZNZXHXNYAQZXGYIBYCSWFYCMZWGYEWHYFXFYHWFYCEUE
      WFYCJUFYKWIYGWNWFYCUGUHUIUJYDYJXIXPFUKGUKULUMYJXJXQQZXKXRQZXMXTQZNYIXJXKX
      MXQXRXTUNYLYEYMYFYNYHYLYEXIXPEUOUPXIXPUQYNWNCXLRZXSXLRZYGXMWNYOMXTDXLCURU
      AXTYOYPMXMCXSXLUSUTYPYGMYNYPXLXSRYGXSXLVAXIXPVBVCUMVDVEVFVGVHVIVJVIVKXDXN
      BFWFXIMZWGXJWHXKXCXMWFXIEUEWFXIJUFYQWIXLDWFXIUGUHUIVLWTYABGWFXPMZWGXQWHXR
      WSXTWFXPEUEWFXPJUFYRWIXSCWFXPUGUHUIVLVMVNWPXEXHWOVOXAWPWOXHABWNSEUCVPVQVR
      VSVTVKVJVTWAWBWCWDWE $.
      $( [2-Sep-2008] $) $( [31-Oct-2007] $)
  $}

  ${
    $d A a x y $.
    $( Condition required for a nonempty finite intersection. $)
    fine $p |- ( A =/= (/)
               -> { x | E. y ( y (_ A /\ y e. Fin /\ x = |^| y ) } =/= (/) ) $=
      ( va cv wcel wex wss cfn cint wceq w3a c0 wne cab ax-17 csn wa cvv wi
      snex sseq1 eleq1 inteq eqeq2d 3anbi123d cla4egv ax-mp snssi adantl snfi
      a1i visset intsn eqcomi eqeq2i biimpi adantr syl3anc ex a4ime 19.23aiv
      n0 abn0 3imtr4i ) DEZCFZDGBEZCHZVHIFZAEZVHJZKZLZBGZAGZCMNVOAOMNVGVPDVGVOA
      DVGAPVKVFKZVGVOVFQZCHZVRIFZVKVRJZKZVOVQVGRZVRSFVSVTWBLZVOTVFUAVNWDBVRSVHV
      RKZVIVSVJVTVMWBVHVRCUBVHVRIUCWEVLWAVKVHVRUDUEUFUGUHVGVSVQVFCUIUJVTWCVFUKU
      LVQWBVGVQWBVFWAVKWAVFVFDUMUNUOUPUQURUSUTVAVBDCVCVOAVDVE $.
      $( [2-Sep-2008] $) $( [16-Nov-2007] $)
  $}

  ${
    $d A x y  $.
    $( Any element of ` A ` is the intersection of a finite subclass of
       ` A ` . $)
    abfi $p |- A
               (_ { x | E. y ( y (_ A /\ y e. Fin /\ x = |^| y ) } $=
      ( cv wss cfn wcel cint wceq w3a wex cab wi ssab csn cpw wa wrex snfi
      visset intsn eqcomi eleq1 inteq eqeq2d anbi12d rcla4ev ex mp2ani snelpw
      df-rex elpw anbi1i 3anass bitr4i exbii bitr2i 3imtr4i mpgbir ) CBDZCEZUTF
      GZADZUTHZIZJZBKZALEVCCGZVGMAVGACNVCOZCPZGZVBVEQZBVJRZVHVGVKVIFGZVCVIHZIZV
      MVCSVOVCVCATZUAUBVKVNVPQZVMVLVRBVIVJUTVIIZVBVNVEVPUTVIFUCVSVDVOVCUTVIUDUE
      UFUGUHUIVCCVQUJVMUTVJGZVLQZBKVGVLBVJUKWAVFBWAVAVLQVFVTVAVLUTCBTULUMVAVBVE
      UNUOUPUQURUS $.
      $( [2-Sep-2008] $) $( [18-Nov-2007] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( If ` A ` is the intersection of a finite set of elements of ` B ` then
       ` A (_ U. B ` . $)
    fiiu $p |- ( A e. { x | E. y ( y (_ B /\ y e. Fin /\ x = |^| y ) }
           -> A (_ U. B ) $=
      ( cv wss cfn wcel cint wceq w3a wex cab cuni cvv wn nvel eleq1 biimpcd
      mtoi wi spfi eqeq1 notbid biimpd c0 int0 eqeq2 bicomd inteq con3i sseq1
      eqcoms wne intssuni2 df-ne sylan2br ancoms 3adant2 syl5com 3exp com24
      syl syl6bi ax-mp syl6com com13 pm2.43i com4t 3imp 19.23aiv mpid ) CBEZDFZ
      VMGHZAEVMIZJKBLAMZHZCDNZFZVRVRCOJZPZVTVRWAOVQHZVQQWAVRWCCOVQRSTVRVRVNVOCV
      PJZKZBLWBVTUAZABCVQDUBWEWFBVNVOWDWFWDWBVNVOVTWDWBVNVOVTUAUAZUAWBWDWDWGWDW
      BVPOJZPZWDWGUAZWDWBWIWDWAWHCVPOUCUDUEUFIZOJZWIWJUAUGWLWIVPWKJZPZWJWLWHWMW
      LWMWHWKOVPUHUIUDWNVMUFJZPZWJWOWMVMUFUJUKWPVOVNWDVTWPVOVNWDVTUAWDVPVSFZVTW
      PVOVNKWQVTUAVPCVPCJWQVTVPCVSULUEUMWPVNWQVOVNWPWQVNVMUFUNWQWPVMDUOVMUFUPUQ
      URUSUTVAVBVCVDVEVFVGVHVIVJVKVDVLVH $.
      $( [2-Sep-2008] $) $( [30-Nov-2007] $)
  $}


  $( An intersection with a member of a powerset belongs to this powerset. $)
  inpws1 $p |- ( A e. P~ C -> ( A i^i B ) e. P~ C ) $=
    ( cin cvv wcel cpw inex1g wss elpwg elpwi ssinss1 syl syl5bir mpcom ) ABDZE
    FZACGZFZPRFZABRHQTPCIZSPCEJSACIUAACKABCLMNO $.
    $( [28-Oct-2007] $) $( [25-Sep-2007] $)

  $( An intersection with a member of a powerset belongs to this powerset. $)
  inpws2 $p |- ( B e. P~ C -> ( A i^i B ) e. P~ C ) $=
    ( cpw wcel cin inpws1 incom syl5eqel ) BCDZEBAFJABFBACGABHI $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)

  ${
    $d v x $.  $d w x $.
    $d x y $.  $d x z $.
  $( Structure of the class abstraction used by ` Alg ` , ` Cat ` and
     ` Ded ` . $)
  stcat $p |- { x | E. y E. z E. v E. w ( x = <. <. y , z >. , <. v , w >. >.
       /\ ph ) } (_ ( ( V X. V ) X. ( V X. V ) ) $=
    ( cv cop wceq wa wex cvv cxp wcel opex opelxp visset mpbir2an eleq1 mpbiri
    adantr 19.23aivv abssi ) BGZCGZDGZHZFGZEGZHZHZIZAJZEKFKZDKCKBLLMZUOMZUNUDUP
    NZCDUMUQFEULUQAULUQUKUPNZURUGUONZUJUONZUGUJUOUOUHUIOPUSUELNUFLNUEUFLLDQZPCQ
    VARUTUHLNUILNUHUILLEQZPFQVBRRUDUKUPSTUAUBUBUC $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)
  $}

  $( A theorem of the ~ 1st2nd family. $)
  11st22nd $p |- ( ( ( Rel B /\ Rel dom B /\ Rel ran B ) /\ A e. B ) ->
     A = <. <. ( 1st ` ( 1st ` A ) ) , ( 2nd ` ( 1st ` A ) ) >. ,
            <. ( 1st ` ( 2nd ` A ) ) , ( 2nd ` ( 2nd ` A ) ) >. >. ) $=
    ( wrel cdm crn w3a wcel wa c1st cfv c2nd cop wceq 1st2nd 3ad2antl1 wi
    1stdm sylan2 exp32 a1i com13 3imp1 2ndrn com3r opeq12d eqtrd ) BCZBDZCZBEZC
    ZFABGZHZAAIJZAKJZLZUNIJUNKJLZUOIJUOKJLZLUGUIULAUPMUKABNOUMUNUQUOURUGUIUKULU
    NUQMZUKUIUGULUSPZUIUGUTPPUKUIUGULUSUIUNUHGUSUGULHZUNUHNABQRSTUAUBUGUIUKULUO
    URMZUIUKUGULVBPZUKUGVCPPUIUKUGULVBUKUOUJGVBVAUOUJNABUCRSTUDUBUEUF $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)

  ${ $d A x $.
    $( The union of a part of a powerset belongs to it. $)
    ump $p |- ( A e. B -> U. { x e. P~ A | ph } e. P~ A ) $=
      ( wcel cpw crab cuni wss ssrab2 sspwuni mpbi elpw2g mpbiri ) CDEABCFZGZHZ
      OEQCIZPOIRABOJPCKLQCDMN $.
      $( [29-Nov-2007] $) $( [16-Nov-2007] $)
  $}

  ${
    $( ` { A , A } ` has only one element. $)
    boe $p |- ( A e. B -> { A , A } ~~ 1o ) $=
      ( wcel cpr csn c1o cen wceq dfsn2 eqcomi a1i ensn1g eqbrtrd ) ABCZAADZAEZ
      FGOPHNPOAIJKABLM $.
      $( [15-Feb-2010] $) $( [15-Feb-2010] $)

  $}

  $( Moving an element ` B ` out from the intersection of a class ` A ` . $)
  moec $p |- ( B e. A -> |^| A = ( B i^i |^| ( A \ { B } ) ) ) $=
    ( wcel cint csn cdif cun cin difsnid eqcomd inteqd wceq intun a1i cvv
    elisset c0 cif sneq id eqeq12d 0ex elimel intsn dedth syl ineq2d incom
    syl6eq 3eqtrd ) BACZADABEZFZULGZDZUMDZULDZHZBUPHZUKAUNUKUNAABIJKUOURLUKUMUL
    MNUKURUPBHUSUKUQBUPUKBOCZUQBLZBAPUTVAUTBQRZEZDZVBLBQBVBLZUQVDBVBVEULVCBVBSK
    VETUAVBBQOUBUCUDUEUFUGUPBUHUIUJ $.
    $( [29-Nov-2007] $) $( [29-Nov-2007] $)


  ${
    $( A function's value belongs to its range. A more general version
       of ~ fnoprvrn .  To be used with partial operations. $)
    fnoprvrn2 $p |- ( ( Fun F /\ <. A , B >. e. dom F ) ->
      ( A F B ) e. ran F ) $=
      ( wfun cop cdm wcel wa cfv crn co fvelrn df-opr syl5eqel ) CDABEZCFGHOCIC
      JABCKOCLABCMN $.
      $( [10-Mar-2008] $) $( [10-Mar-2008] $)
  $}

  $( The intersection of a pair is the intersection of its members.  Closed
     form of ~ intpr .  Theorem 71 of [Suppes] p. 42. $)
  intprd $p |- ( ( A e. V /\ B e. V ) -> |^| { A , B } = ( A i^i B ) ) $=
    ( cvv wcel cpr cint cin wceq c0 cif preq1 inteqd ineq1 eqeq12d preq2 ineq2
    0ex elimel intpr dedth2h ) ACDZBCDZABEZFZABGZHUAAIJZBEZFZUFBGZHUFUBBIJZEZFZ
    UFUJGZHABIIAUFHZUDUHUEUIUNUCUGAUFBKLAUFBMNBUJHZUHULUIUMUOUGUKBUJUFOLBUJUFPN
    UFUJAICQRBICQRST $.
    $( [27-Apr-2008] $) $( [27-Apr-2008] $)

  ${
    $d A a b x y $.  $d B a b x y $.  $d L a b x y $.
    ficli.1 $e |- F = { x | E. y ( y (_ L /\ y e. Fin /\ x = |^| y ) } $.
    $( The class of finite intersections of a class ` L ` are closed under
       intersection. $)
    ficli $p |- ( ( A e. F /\ B e. F ) -> ( A i^i B ) e. F ) $=
      ( va vb wcel wa cin cv wss cfn cint wceq w3a wex cab spfi biimpd
      im2anan9 wi cpw wrex cun an6 unss biimpi ancoms visset unex elpw sylibr
      unfi ineq12 intun syl6eqr anim12i 3impb sylbi eleq1 inteq eqeq2d anbi12d
      rcla4ev syl bicomi 3anbi1i 3anass bitri exbii df-rex bitr4i ex 19.23aiv
      com12 imp sseq1 3anbi123d cbvexv syl2anb syl6 pm2.43i eleq2i cvv wb
      inex1g adantr syl5bb mpbird ) CEJZDEJZKZCDLZEJZBMZFNZWROJZWPWRPZQZRZBSZCW
      SWTAMXAQRBSATZJZDXEJZXDWMWNXFXGKZXDXHXHWSWTCXAQZRZBSZWSWTDXAQZRZBSZKXDXFX
      FXKXGXGXNXFXFXKABCXEFUAUBXGXGXNABDXEFUAUBUCHMZFNZXOOJZCXOPZQZRZHSZIMZFNZY
      BOJZDYBPZQZRZISZXDXKXNYAYHXDXTYHXDUDHYHXTXDYGXTXDUDIYGXTXDYGXTKZWTXBKZBFU
      EZUFZXDYIXOYBUGZYKJZYMOJZWPYMPZQZKZKZYLYIYCXPKZYDXQKZYFXSKZRYSYCYDYFXPXQX
      SUHYTUUAUUBYSYTYNUUAUUBKYRYTYMFNZYNXPYCUUCXPYCKUUCXOYBFUIUJUKYMFXOYBHULIU
      LUMUNUOUUAYOUUBYQXQYDYOXOYBUPUKXSYFYQXSYFKWPXRYELYPCXRDYEUQXOYBURUSUKUTUT
      VAVBYJYRBYMYKWRYMQZWTYOXBYQWRYMOVCUUDXAYPWPWRYMVDVEVFVGVHXDWRYKJZYJKZBSYL
      XCUUFBXCUUEWTXBRUUFWSUUEWTXBUUEWSWRFBULUNVIVJUUEWTXBVKVLVMYJBYKVNVOUOVPVQ
      VRVQVSXJXTBHWRXOQZWSXPWTXQXIXSWRXOFVTWRXOOVCUUGXAXRCWRXOVDVEWAWBXMYGBIWRY
      BQZWSYCWTYDXLYFWRYBFVTWRYBOVCUUHXAYEDWRYBVDVEWAWBWCWDWEEXECGWFEXEDGWFWCWO
      WPXEJZXDWQWOWPWGJZUUIXDWHWMUUJWNCDEWIWJABWPWGFUAVHEXEWPGWFWKWL $.
      $( [2-Sep-2008] $) $( [27-Apr-2008] $)
  $}

  $( If an intersection is not empty its operands are not empty. $)
   neiopne $p |- ( ( A i^i B ) =/= (/) -> ( A =/= (/) /\ B =/= (/) ) ) $=
     ( cin c0 wceq wn wo wne wa ineq1 incom eqtr in0 syl6eq mpan2 syl ineq2
     jaoi con3i df-ne neanior 3imtr4i ) ABCZDEZFADEZBDEZGZFUCDHADHBDHIUGUDUEUDU
     FUEUCDBCZEZUDADBJUIUHBDCZEZUDDBKUIUKIUCUJDUCUHUJLBMNOPUFUCADCDBDAQAMNRSUCD
     TADBDUAUB $.
     $( [27-Apr-2008] $) $( [27-Apr-2008] $)

  $( Image of a pre-image. $)
  f2imacnv $p |- ( ( F : A -1-1-onto-> B /\ C (_ B ) ->
                 ( F " ( `' F " C ) ) = C ) $=
    ( wf1o wss ccnv cima wceq wf1 wi f1ocnv f1of1 wa f1imacnv imacnvcnv
    syl5eqr ex 3syl imp ) ABDEZCBFZDDGZCHZHZCIZUABAUCEBAUCJZUBUFKABDLBAUCMUGUBU
    FUGUBNUCGUDHCUEBACUCODUDPQRST $.
    $( [1-Jun-2008] $) $( [1-Jun-2008] $)


  $( Symmetrical equality of the images and of their antecedents when
     the mapping is one to one. $)
  oooeqim2 $p |- ( ( F : A -1-1-> B /\ X (_ A /\ Y (_ A )
     -> ( ( F " X ) = ( F " Y ) <-> X = Y ) ) $=
    ( wf1 wss w3a cima wceq ccnv wa wi f1imacnv ex anim12d 3impib eqtr syl
    com23 eqcoms imp imaeq2 syl5 impbid1 ) ABCFZDAGZEAGZHZCDIZCEIZJZDEJZUICKZUJ
    IZUNUKIZJZUMULUIUODJZUPEJZLZUQUMMZUFUGUHUTUFUGURUHUSUFUGURABDCNOUFUHUSABECN
    OPQURUSVAUSVAMDUODUOJZUQUSUMVBUQUSUMMZVBUQLDUPJZVCDUOUPRVDUSUMDUPEROSOTUAUB
    SUJUKUNUCUDDECUCUE $.
    $( [1-Jun-2008] $) $( [1-Jun-2008] $)

  $( The empty set is finite. $)
  emfin $p |- (/) e. Fin $=
    ( c0 com wcel wss cfn peano1 ssid ssnnfi mp2an ) ABCAADAECFAGAAHI $.
    $( [2-Sep-2008] $) $( [14-Jul-2008] $)

  $( A  theorem about things which don't exist ` V ` and
     ` ( V X. V ) ` . $)
  vxveqv $p |- ( V X. V ) =/= V $=
    ( vx cvv cxp wne wceq wn wpss wss dfpss3 ssv cv wcel wa wex c0 0ex 0nelxp
    eleq1 notbid anbi12d cla4ev mp2an nss mpbir mpbir2an pssv mpbi df-ne ) BBCZ
    BDUIBEFZUIBGZUJUKUIBHBUIHFZUIBIUIJULAKZBLZUMUILZFZMZANZOBLZOUILZFZURPBBQUQU
    SVAMAOPUMOEZUNUSUPVAUMOBRVBUOUTUMOUIRSTUAUBABUIUCUDUEUIUFUGUIBUHUD $.
    $( [23-Sep-2008] $) $( [22-Sep-2008] $)

  $( The double union of the converse of a class ` A ` is included in the
     double union of the class. $)
  ducidu $p |- U. U. `' A (_ U. U. A $=
    ( ccnv cuni cdm crn cun unidmrn dmrnssfld eqsstri ) ABCCADAEFACCAGAHI $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  $( if ` R ` is a relation, its double union equals the double union of its
     converse. $)
  relcnvfld $p |- ( Rel R -> U. U. R = U. U. `' R ) $=
    ( wrel cuni cdm crn cun ccnv relfld unidmrn syl6eqr ) ABACCADAEFAGCCAHAIJ
    $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)

  ${ $d R x $.
  $( Domain and range of a reflexive relation are equal. $)
  domrngref $p |- ( ( Rel R /\ A. x e. U. U. R x R x ) -> dom R = ran R ) $=
    ( wrel cv wbr cuni wral wa cdm wcel crn wb wal wceq wi cun relfld eqcomd
    eleq2d biimpd imim1d wo elun imbi1i jaob visset brelrn imim2i adantr
    breldm adantl impbid sylbi syl6 19.20dv df-ral syl5ib imp dfcleq sylibr )
    BCZADZVBBEZABFFZGZHVBBIZJZVBBKZJZLZAMZVFVHNVAVEVKVAVBVDJZVCOZAMVKVEVAVMVJAV
    AVMVBVFVHPZJZVCOZVJVAVOVLVCVAVOVLVAVNVDVBVAVDVNBQRSTUAVPVGVIUBZVCOZVJVOVQVC
    VBVFVHUCUDVRVGVCOZVIVCOZHZVJVGVCVIUEWAVGVIVSVGVIOVTVCVIVGVBVBBAUFZWBUGUHUIV
    TVIVGOVSVCVGVIVBVBBWBUJUHUKULUMUMUNUOVCAVDUPUQURAVFVHUSUT $.
    $( [7-Oct-2008] $) $( [6-Oct-2008] $)

  $( The domain of a reflexive relation is equal to its field . $)
  domfldref $p |- ( ( Rel R /\ A. x e. U. U. R x R x ) -> dom R = U. U. R ) $=
    ( wrel cv wbr cuni wral cdm wceq wa crn cun wi domrngref eqtr unidm
    syl6req ex uneq2 eqcomd syl5com syl relfld anabsi5 ) BCZADZUFBEABFFZGZBHZUG
    IZUEUHJZUGUIBKZLZIZUJUEUKUIULIZUNUJMABNUNUMUIUILZIZUJUOUNUQUJUNUQJUGUPUIUGU
    MUPOUIPQRUOUPUMUIULUISTUAUBBUCUAUD $.
    $( [7-Oct-2008] $) $( [6-Oct-2008] $)

  $}

  ${ $d x R $. $d x A $.
    domintreflem.1 $e |- A e. V $.
    $( Lemma for ~ domintref . $)
    domintreflem $p |- ( ( Rel R /\ A. x e. U. U. R x R x ) ->
      ( A e. dom R <-> <. A , A >. e. R ) ) $=
      ( wrel cv wbr cuni wral wa cdm wcel cop wceq wi domfldref eleq2 biimpd
      wb breq12 anidms df-br syl6bb rcla4cv adantl syl9 mpcom opeldm impbid1 )
      CEZAFZUKCGZACHHZIZJZBCKZLZBBMCLZUPUMNZUOUQUROACPUSUQBUMLZUOURUSUQUTUPUMBQ
      RUNUTUROUJULURABUMUKBNZULBBCGZURVAULVBSUKBUKBCTUABBCUBUCUDUEUFUGBBCDUHUI
      $.
      $( [7-Oct-2008] $) $( [6-Oct-2008] $)
  $}

  ${ $d R x y $. $d S x y $.
    $( The domain of the intersection of two reflexive relations is the
       intersection of their domains. Compare with ~ dmin . $)
    domintref $p |- ( ( ( Rel R /\ A. x e. U. U. R x R x ) /\
     ( Rel S /\ A. x e. U. U. S x S x ) ) ->
      dom ( R i^i S ) = ( dom R i^i dom S ) ) $=
      ( vy wrel cv wbr cuni wral wa cin cdm wcel cop wb visset domintreflem wi
      dmin sseli elin bi1 com12 im2anan9 incom eleq2i opeldm sylbi sylbir syl6
      ancoms impbid2 ex syl mpan9 eqrdv ) BEAFZUQBGABHHIJZCEUQUQCGACHHIJZJDBCKZ
      LZBLZCLZKZURDFZVBMZVEVENZBMZOZUSVEVAMZVEVDMZOZAVEBDPZQUSVEVCMZVGCMZOZVIVL
      RAVECVMQVPVIVLVPVIJZVJVKVAVDVEBCSTVKVQVJVKVFVNJVQVJRZVEVBVCUAVNVFVRVNVFJV
      QVOVHJZVJVNVPVOVFVIVHVPVNVOVNVOUBUCVIVFVHVFVHUBUCUDVSVGCBKZMZVJVGCBUAWAVG
      UTMVJVTUTVGCBUEUFVEVEUTVMUGUHUIUJUKUHUCULUMUNUOUP $.
      $( [7-Oct-2008] $) $( [6-Oct-2008] $)
  $}

  ${ $d A x y $. $d R x y $.
  $( If ` R ` is a reflexive relation and ` A ` a part of its field,
     ` A ` is a part of the image of ` A ` by ` R ` . $)
  imgfldref2 $p |- ( ( A. x e. U. U. R x R x /\ A (_ U. U. R )
    -> A (_ ( R " A ) ) $=
    ( vy cv wbr cuni wral wss wa wrex cab cima wcel wi wal hbra1 ax-17 hban
    ssel ra4 breq1 rcla4e expcom syl6com com24 pm2.43i com3l imp 19.21ai ssab
    sylibr dfima2 syl6ssr ) AEZUOCFZACGGZHZBUQIZJZBDEZUOCFZDBKZALZCBMUTUOBNZVCO
    ZAPBVDIUTVFAURUSAUPAUQQUSARSURUSVFVEURUSVCVEURUSVCOOVEUSURVEVCUSVEUOUQNZURV
    FOBUQUOTURVGUPVFUPAUQUAVEUPVCVBUPDUOBUPDRVAUOUOCUBUCUDUEUEUFUGUHUIUJVCABUKU
    LDACBUMUN $.
    $( [3-Aug-2009] $) $( [3-Jul-2009] $)
  $}

  ${
    $d R x $.
    $( The converse of a reflexive relation is reflexive. $)
    cnvref $p |- ( A. x e. ( dom R u. ran R ) x R x <->
    A. x e. ( dom `' R u. ran `' R ) x `' R x ) $=
      ( cv wbr cdm crn cun wral ccnv wcel wb wa brcnvg bicomd anidms ralbiia
      wceq cuni unidmrn eqcomi wrel relcnv relfld ax-mp eqtri raleq1 bitri ) AC
      ZUHBDZABEBFGZHUHUHBIZDZAUJHZULAUKEUKFGZHZUIULAUJUHUJJZUIULKUPUPLULUIUHUHU
      JUJBMNOPUJUNQUMUOKUJUKRRZUNUQUJBSTUKUAUQUNQBUBUKUCUDUEULAUJUNUFUDUG $.
      $( [3-Aug-2009] $) $( [31-Jul-2009] $)
  $}

  $( The intersection of two relations is equivalent to a conjunction
     of those relations. $)
  int2rel $p |- ( A ( R i^i S ) B <-> ( A R B /\ A S B ) ) $=
    ( cop cin wcel wa wbr elin df-br anbi12i 3bitr4i ) ABEZCDFZGNCGZNDGZHABOIAB
    CIZABDIZHNCDJABOKRPSQABCKABDKLM $.
    $( [7-Oct-2008] $) $( [6-Oct-2008] $)

  $( ` <. A , A >. ` belongs to a restriction of the identity class iff ` A `
     belongs to the restricting class. $)
  eltids $p |- ( A e. C -> ( A e. B <-> <. A , A >. e. ( I |` B ) ) ) $=
    ( wcel cop cid wa cres wbr ididg df-br sylib biantrurd opelresg bitr4d ) AC
    DZABDZAAEZFDZQGRFBHDPSQPAAFISACJAAFKLMAAFBCNO $.
    $( [17-Nov-2008] $) $( [27-Oct-2008] $)

  ${ $d R u x y $.
    $( Replace U. U. R by ( dom R u. ran R ) ? $)
    $( Two ways to state a relation is reflexive. (Adapted from Tarski.) $)
    ref3w $p |- ( A. x e. U. U. R x R x <->
        ( I |` U. U. R ) (_ R ) $=
      ( vy vu cv wbr cuni wral wcel wi wal cop cid cres wss df-ral cvv wb
      visset eltids ax-mp df-br imbi12i albii ralidm ralv bitri cxp pm2.27 wa
      opelresg wceq ideq opeq2 eleq1d biimpcd syl6 syl6bi pm2.43i com3r sylbi
      sylbir imp r19.21aiv a4s r19.20si eleq1 imbi12d ralxp sylibr pm3.31 wrel
      relres df-rel mpbi sseli ancri syl5 19.20i syl dfss2 ssel 19.21aiv
      impbii 3bitri ) AEZWFBFZABGGZHWFWHIZWGJZAKWFWFLZMWHNZIZWKBIZJZAKZWLBOZWGA
      WHPWJWOAWIWMWGWNWFQIZWIWMRASZWFWHQTUAWFWFBUBUCUDWPWQWPCEZWLIZWTBIZJZCKZWQ
      WPWOAQHZAQHZXDXFXEWPWOAQUEWOAUFUGXFXCCQQUHZHZXDXFWFDEZLZWLIZXJBIZJZDQHZAQ
      HXHXEXNAQXEWRWOJZAKXNWOAQPXOXNAWRXOXNJWSWRXOWOXNWRWOUIWOXMDQXIQIZXKWOXLXP
      XKXJMIZWIUJWOXLJZWFXIMWHQUKXQWIXRXQWFXIMFZWIXRJZWFXIMUBXSWFXIULZXTWFXIDSU
      MWIWOYAXLWIWOYAXLJZJZWIWIWMYCWFWHWHTWMWOWNYBWMWNUIYAWNXLYAWKXJBWFXIWFUNUO
      UPUQURUSUTVAVBVCURUTVDUQUAVEVAVFXCXMCADQQWTXJULXAXKXBXLWTXJWLVGWTXJBVGVHV
      IVJXHWTXGIZXCJZCKXDXCCXGPYEXCCYEYDXAUJXBXAYDXAXBVKXAYDWLXGWTWLVLWLXGOMWHV
      MWLVNVOVPVQVRVSVAVTVBCWLBWAVJWQWOAWLBWKWBWCWDWE $.
      $( [17-Nov-2008] $) $( [27-Oct-2008] $)
  $}

  $( Two ways to say that ` A ` and ` B ` partition ` C ` (when ` A ` and
     ` B ` don't overlap and ` A ` is a part of ` C ` ). $)
  twpar2 $p |- ( ( A (_ C /\ ( A i^i B ) = (/) )
     -> ( ( A u. B ) = C <-> ( C \ A ) = B ) ) $=
    ( wss cin c0 wceq wa cun cdif wi uncom eqtr eqcom biimpi difeq1 difun2
    incom eqeq1i disj3 bitri expcom eqcoms sylbi syl5com mpan2 syl 3syl mpan
    com12 adantl difss sseq1 unss syl6bi mpi adantr imp eqimss ssundif sylibr
    adantlr eqssd ex impbid ) ACDZABEZFGZHZABIZCGZCAJZBGZVHVKVMKVFVKVHVMBAIZVJG
    ZVKVHVMKZBALVOVKHVNCGZCVNGZVPVNVJCMVQVRVNCNOVRVLVNAJZGZVPCVNAPVTVSBAJZGZVPB
    AQVHVLWAGZVMVTWBHVHBWAGZWCVMKZVHBAEZFGWDVGWFFABRSBATUAWEWABWCWABGVMVLWABMUB
    UCUDVLVSWAMUEUFUGUHUIUJUKVIVMVKVIVMHVJCVIVMVJCDZVFVMWGKVHVMVFWGVMVLCDZVFWGK
    ZCAULVMWHBCDZWIVLBCUMVFWJWGVFWJHWGABCUNOUBUOUPUJUQURVFVMCVJDZVHVFVMHVLBDZWK
    VMWLVFVLBUSUKCABUTVAVBVCVDVE $.
    $( [17-Nov-2008] $) $( [16-Nov-2008] $)

  $( Equality theorem for functions. $)
  feq123 $p |- ( ( F = G /\ A = C /\ B = D ) ->
    ( F : A --> B <-> G : C --> D ) ) $=
    ( wceq w3a wf wb feq1 3ad2ant1 feq23 3adant1 bitrd ) EFGZACGZBDGZHABEIZABFI
    ZCDFIZPQSTJRABEFKLQRTUAJPABCDFMNO $.
    $( [17-Nov-2008] $) $( [16-Nov-2008] $)

  $( A class whose union is finite is finite. $)
  unfinsef $p |- ( U. A e. Fin -> A e. Fin ) $=
    ( cuni cfn wcel cpw pwfi wss pwuni ssfi mpan2 sylbi ) ABZCDLEZCDZACDZLFNAMG
    OAHMAIJK $.
    $( [22-Dec-2008] $) $( [22-Dec-2008] $)

  ${
    fveq12d.1 $e |- ( ph -> F = G ) $.
    fveq12d.2 $e |- ( ph -> A = B ) $.
    $( Equality deduction for function value. $)
    fveq12d $p |- ( ph -> ( F ` A ) = ( G ` B ) ) $=
      ( cfv fveq1d fveq2d eqtrd ) ABDHBEHCEHABDEFIABCEGJK $.
      $( [22-Dec-2008] $) $( [22-Dec-2008] $)
  $}

  ${
    opreq123d.1 $e |- ( ph -> F = G ) $.
    opreq123d.2 $e |- ( ph -> A = B ) $.
    opreq123d.3 $e |- ( ph -> C = D ) $.
    $( Equality deduction for operation value. $)
    opreq123d $p |- ( ph -> ( A F C ) = ( B G D ) ) $=
      ( co opreqd opreq1d opreq2d 3eqtrd ) ABDFKBDGKCDGKCEGKAFGBDHLABCDGIMADECG
      JNO $.
      $( [22-Dec-2008] $) $( [22-Dec-2008] $)
  $}

  $( If the domain of a bijection is finite its range is finite and
     reciprocally. $)
  f1ofi $p |- ( F : A -1-1-onto-> B -> ( A e. Fin <-> B e. Fin ) ) $=
    ( wf1o cfn wcel wfo fofi ex f1ofo syl5com ccnv wi f1ocnv expcom 3syl
    impbid ) ABCDZAEFZBEFZSABCGZTRSUATABCHIABCJKRBACLZDBAUBGZTSMABCNBAUBJTUCSBA
    UBHOPQ $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  ${ $d A B F x $.
    f1fi.1 $e |- F e. C $.
    $( If a 1-to-1 function has a finite codomain its domain is finite. $)
    f1fi $p |- ( ( B e. Fin /\ F : A -1-1-> B ) -> A e. Fin ) $=
      ( vx wf1 cfn wcel cv wf1o wss wa wex wi elisseti f11o f1ofi ssfi
      syl5cbir ex com13 imp 19.23aiv sylbi impcom ) ABDGZBHIZAHIZUGAFJZDKZUJBLZ
      MZFNUHUIOZFABDDCEPQUMUNFUKULUNUHULUKUIUHULUKUIOUKUIUJHIUHULMAUJDRBUJSTUAU
      BUCUDUEUF $.
      $( [3-Aug-2009] $) $( [31-Jul-2009] $)
  $}

  $( Field of a square cross product. $)
  fldsqcp2 $p |- U. U. ( A X. A ) = A $=
    ( c0 wceq cxp cuni xpeq1 xp0r syl6eq wi unieq unieqd uni0 unieqi eqtri wa
    eqtr expcom eqcoms syl5com mpan2 syl mpcom wne wn xpnz cun unixp unidm
    sylbi df-ne sylancbr pm2.61i ) ABCZAADZEZEZACZUNBCZUMUQUMUNBADBABAFAGHURUPB
    EZEZCZUMUQIZURUOUSUNBJKVAUTBCZVBUTUSBUSBLMLNUMUPBCZUQVAVCOVDUQIBAVDBACUQUPB
    APQRUPUTBPSTUAUBABUCZVEUQUMUDVEVEOUNBUCZUQAAUEVFUPAAUFAAAUGAUHHUIABUJZVGUKU
    L $.
    $( [3-Nov-2009] $) $( [10-Oct-2009] $)

  $( To be deleted. $)
  $( Field of a square cross product. $)
  fldsqcp $p |- ( A =/= (/) -> U. U. ( A X. A ) = A ) $=
    ( c0 wne cxp cuni wceq wa xpnz cun unixp unidm syl6eq sylbi anidms ) ABCZAA
    DZEEZAFZOOGPBCZRAAHSQAAIAAAJAKLMN $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)


  ${ $d A x y z $.
  $( A square cross product ` ( A X. A ) ` is a transitive relation. $)
  cpref $p |- ( ( A X. A ) o. ( A X. A ) ) (_ ( A X. A ) $=
    ( vx vy vz cxp ccom wss cv wbr wa wi wal wcel visset brxp biimpri expcom
    adantl sylbi com12 adantr imp ax-gen gen2 cotr mpbir ) AAEZUGFUGGBHZCHZUGIZ
    UIDHZUGIZJUHUKUGIZKZDLZCLBLUOBCUNDUJULUMUJUHAMZUIAMZJULUMKZUHUIAACNOUPURUQU
    LUPUMULUQUKAMZJUPUMKZUIUKAADNZOUSUTUQUPUSUMUMUPUSJUHUKAAVAOPQRSTUASUBUCUDBC
    DUGUEUF $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)
  $}

  $( A square cross product ` ( A X. A ) ` is reflexive. $)
  scprefat $p |- ( I |` U. U. ( A X. A ) ) (_ ( A X. A ) $=
    ( cid cxp cuni cres wss c0 wceq cun unidm unixpss sseq2 ss0 wf1o f1oi wf
    f1of fssxp syl ax-mp reseq2 sseq1d mpbiri syl6bi mp2 xpeq1 xpeq2 eqtrd
    unieqd sseq12d wne fldsqcp sseq1 pm2.61ine ) BAACZDZDZEZUOFZAGAGHZUSBGGCZDZ
    DZEZVAFZGGIZGHZVCVFFZVEGJGGKVGVHVCGFZVEVFGVCLVIVCGHZVEVCMVJVEBGEZVAFZGGVKNZ
    VLGOVMGGVKPVLGGVKQGGVKRSTVJVDVKVAVCGBUAUBUCSUDUEUTURVDUOVAUTUQVCHURVDHUTUPV
    BUTUOVAUTUOGACVAAGAUFAGGUGUHZUIUIUQVCBUASVNUJUCAGUKUQAHZUSAULVOURBAEZHZUSUQ
    ABUAVQUSVPUOFZAAVPNZVRAOVSAAVPPVRAAVPQAAVPRSTURVPUOUMUCSSUN $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  $( A square cross product ` ( A X. A ) ` is reflexive. $)
  scprefat2 $p |- ( I |` A ) (_ ( A X. A ) $=
    ( cid cres cxp wss c0 wceq wa ssid sseq12 mpbiri reseq2 res0 syl6eq xpid11
    xp0r eqeq2i biimpi sylbir sylanc wne cuni fldsqcp scprefat sseq1 eqcoms
    syl pm2.61ine ) BACZAADZEZAFUIFGZUJFGZUKAFGZULUMHUKFFEFIUIFUJFJKUNUIBFCFAFB
    LBMNUNUJFFDZGZUMAFOUPUMUOFUJFPQRSTAFUAUJUBUBZAGZUKAUCURBUQCZUIGUKUQABLUKUIU
    SUIUSGUKUSUJEAUDUIUSUJUEKUFUGUGUH $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)


  $( A square cross product ` ( A X. A ) ` is symmetric. $)
  sqpsym $p |- `' ( A X. A ) (_ ( A X. A ) $=
    ( cxp ccnv cnvxp eqimssi ) AABZCFAADE $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  $( A square cross product is an equivalence relation (in general
     it's not a poset). $)
  sqpeq $p |- Er ( A X. A ) $=
    ( cxp wer ccnv ccom cun wss wceq cnvxp cpref wi uneq1 unidm wa eqtr sseq2
    biimpd syl mpan2 unss2 syl5 mp2 df-er mpbir ) AABZCUEDZUEUEEZFZUEGZUFUEHZUG
    UEGZUIAAIAJUJUHUFUEFZGZUIUKUJULUEUEFZHZUMUIKZUFUEUELUOUNUEHZUPUEMUOUQNULUEH
    ZUPULUNUEOURUMUIULUEUHPQRSRUGUEUFTUAUBUEUCUD $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  ${ $d R x y z $. $d S x y z $.
   $( The intersection of two transitive relations is transitive. $)
   inttr $p |- ( ( ( R o. R ) (_ R /\ ( S o. S ) (_ S )
     -> ( ( R i^i S ) o. ( R i^i S ) ) (_ ( R i^i S ) ) $=
     ( vx vy vz ccom wss wa cv cin wbr wi wal cotr 19.26 pm3.27 pm3.26 anim12d
     com12 an4s int2rel syl2anb syl6ibr 19.20i sylbir ex sylbi imp sylibr ) AAF
     AGZBBFBGZHCIZDIZABJZKZUMEIZUNKZHZULUPUNKZLZEMZDMZCMZUNUNFUNGUJUKVCUJULUMAK
     ZUMUPAKZHZULUPAKZLZEMZDMZCMZUKVCLCDEANUKVKVCUKULUMBKZUMUPBKZHZULUPBKZLZEMZ
     DMZCMZVKVCLCDEBNVSVKVCVSVKHVRVJHZCMVCVRVJCOVTVBCVTVQVIHZDMVBVQVIDOWAVADWAV
     PVHHZEMVAVPVHEOWBUTEWBURVGVOHZUSURWBWCVDVLHVEVMHWBWCLZUOUQVDVEVLVMWDWBVFVN
     HWCWBVFVGVNVOVPVHPVPVHQRSTULUMABUAUMUPABUAUBSULUPABUAUCUDUEUDUEUDUEUFUGSUG
     UHCDEUNNUI $.
     $( [3-Aug-2009] $) $( [31-Jul-2009] $)
  $}

  $( If a relation is transitive, its intersection with a square cross product
     is transitive. (taking the intersection of a relation with a square cross
     product is a way to restrict a relation to a subset of its field). $)
  tricptr $p |- ( ( R o. R ) (_ R ->
   ( ( R i^i ( A X. A ) ) o. ( R i^i ( A X. A ) ) ) (_ ( R i^i ( A X. A ) ) ) $=
    ( ccom wss cxp cin cpref inttr mpan2 ) BBCBDAAEZJCJDBJFZKCKDAGBJHI $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)

  $( The members of a 'less than or equal' relationship are extended reals. $)
  mlteqer $p |- ( B e. C -> ( A <_ B -> ( A e. RR* /\ B e. RR* ) ) ) $=
    ( cle wbr wcel cxr wa cop wi df-br cxp clt ccnv cdif df-le eleq2i wn eldif
    opelxpg biimpcd adantr sylbi com12 ) ABDEZBCFZAGFBGFHZUEABIZDFZUFUGJZABDKUI
    UHGGLZMNZOZFZUJDUMUHPQUNUHUKFZUHULFRZHUJUHUKULSUOUJUPUFUOUGABGGCTUAUBUCUCUC
    UD $.
    $( [3-Aug-2009] $) $( [31-Jul-2009] $)


  $( Transitive law for ordering on extended reals ( compare ~ xrletr ). $)
  xrletr2 $p |- ( C e. D ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cle wbr wa wcel wi cxr mlteqer w3a xrletr exp3a 3expia com23 syl6 com3l
    pm2.43i com4l imp impcom com12 ) ABEFZBCEFZGCDHZACEFZUEUDUFUGIZUEUDUHIUFUEU
    EUDUGUFUEBJHZCJHZGUEUDUGIIZBCDKUIUJUKUDUIUJUEUGUDUIUJUEUGIZIZIUIUDUDUMUIUDA
    JHZUIGZUDUMIABJKUOUJUDULUNUIUJUDULIUNUIUJLUDUEUGABCMNOPQRSTUAQTSUBUC $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( Trichotomy law. $)
  letri31 $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A = B <-> ( A <_ B /\ B <_ A ) ) ) $=
    ( cxr wcel wa wceq clt wbr wn cle xrlttri3 ancom syl6bbr xrlenlt wb ancoms
    anbi12d bitr4d ) ACDZBCDZEZABFZBAGHIZABGHIZEZABJHZBAJHZEUAUBUDUCEUEABKUCUDL
    MUAUFUCUGUDABNTSUGUDOBANPQR $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  ${ $d x y $.
    $( domain of ` <_ ` is ` RR* ` . $)
    domleqt $p |- dom <_ = RR* $=
      ( vx vy cle cdm cv wbr wex cab cxr df-dm wceq wcel wb abeq1 cvv wa wi
      visset mlteqer ax-mp pm3.26d 19.23aiv cpnf pnfge pnfxr elisseti breq2
      imbi2d cla4ev 19.37aiv impbii mpgbir eqtri ) CDAEZBEZCFZBGZAHZIABCJURIKUQ
      UNILZMAUQAINUQUSUPUSBUPUSUOILZUOOLUPUSUTPQBRUNUOOSTUAUBUSUPBUSUNUCCFZQZUS
      UPQZBGUNUDVCVBBUCUCIUEUFUOUCKUPVAUSUOUCUNCUGUHUITUJUKULUM $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

   $( The range of ` <_ ` is included in the extended reals. $)
   ranleqt $p |- ran <_ (_ RR* $=
     ( cle crn cxr cxp clt ccnv cdif df-le rneqi wss difss rnss ax-mp c0 wne
     wceq cpnf wcel pnfxr ne0i rnxp sseqtri eqsstri ) ABCCDZEFZGZBZCAUFHIUGUDBZ
     CUFUDJUGUHJUDUEKUFUDLMCNOZUHCPQCRUISCQTMCCUAMUBUC $.
     $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( 'Less or equal to' is a relation. $)
  leqrl $p |- Rel <_ $=
    ( cle wrel cxr cxp clt ccnv cdif relxp reldif ax-mp df-le releqi mpbir ) AB
    CCDZEFZGZBZNBQCCHNOIJAPKLM $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( The field of the 'less or equal to' relationship on the extended real. $)
  fldleqt $p |- U. U. <_ = RR* $=
    ( vx cle cuni cdm crn cun cxr wrel wceq leqrl relfld ax-mp cv wcel wo
    domleqt eleq2i biimpi ranleqt sseli jaoi biimpri orcd impbii uneqri eqtri
    ) BCCZBDZBEZFZGBHUGUJIJBKLAUHUIGAMZUHNZUKUINZOUKGNZULUNUMULUNUHGUKPQZRUIGUK
    STUAUNULUMULUNUOUBUCUDUEUF $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)


  ${ $d A x $. $d B x $.
  $( If an infinite set ` A ` is included in the underlying set of a finite
     cover ` B ` then there exists a set of the cover that contains an infinite
     number of element
  of ` A ` . $)
  isunscov $p |- ( ( -. A e. Fin /\ B e. Fin /\ A (_ U. B ) ->
    E. x e. B -. ( A i^i x ) e. Fin ) $=
    ( cfn wcel wn cuni wss w3a cv cin wrex wral wi wa ciun iunfi iunin2 eleq1i
    uniiun eqcomi ineq2i wceq df-ss eleq1 pm2.24 syl6bi sylbi com12 syl ex
    com24 3imp dfral2 syl5ibr pm2.01bd ) BDEZFZCDEZBCGZHZIZBAJZKZDEZFACLZVBVEAC
    MZVFVFFURUSVAVGVFNZUSURVAVHNUSVGVAURVFUSVGVAURVFNZNZUSVGOACVDPZDEZVJACVDQVL
    BACVCPZKZDEZVJVKVNDACBVCRSVOBUTKZDEZVJVNVPDVMUTBUTVMACTUAUBSVAVQVIVAVPBUCZV
    QVINBUTUDVRVQUQVIVPBDUEUQVFUFUGUHUIUHUHUJUKULUIUMVEACUNUOUP $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  $( If a union is finite, the operands are finite. $)
  unfin $p |- ( ( A u. B ) e. Fin -> ( A e. Fin /\ B e. Fin ) ) $=
    ( cun cfn wcel wss ssun1 ssfi mpan2 ssun2 jca ) ABCZDEZADEZBDEZMALFNABGLAHI
    MBLFOBAJLBHIK $.
    $( [3-Aug-2009] $) $( [10-Jul-2009] $)

  $( If ` A ` is finite, ` ( A \ B ) ` is finite. $)
  finfin $p |- ( A e. Fin -> ( A \ B ) e. Fin ) $=
    ( cfn wcel cdif wss difss ssfi mpan2 ) ACDABEZAFJCDABGAJHI $.
    $( [3-Aug-2009] $) $( [10-Jul-2009] $)

  $( An infinite set ` A ` minus a finite set is infinite. $)
  islfin $p |- ( ( -. A e. Fin /\ B e. Fin  ) -> -. ( A \ B ) e. Fin ) $=
    ( cfn wcel wn cdif wa cun unfi undif1 eleq1i unfin pm3.26d sylbi syl
    expcom con3d impcom ) BCDZACDZEABFZCDZESUBTUBSTUBSGUABHZCDZTUABIUDABHZCDZTU
    CUECABJKUFTSABLMNOPQR $.
    $( [3-Aug-2009] $) $( [10-Jul-2009] $)

  $( If ` A ` belongs to a part of ` C ` then ` A ` belongs to ` C ` . $)
  bepart $p |- ( ( A e. B /\ B e. P~ C ) -> A e. C ) $=
    ( cpw wcel elpwi sseld impcom ) BCDEZABEACEIBCABCFGH $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( You can remove a class only once. $)
  remcon $p |- ( ( A \ B ) \ B ) = ( A \ B ) $=
    ( cun cdif difun1 unidm difeq2i eqtr3i ) ABBCZDABDZBDJABBEIBABFGH $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  ${ $d A f x $.
  $( ~ ac5 with the premisse transformed into an antecedent. $)
  ac5g $p |- ( A e. V ->
     E. f ( f Fn A /\ A. x e. A ( x =/= (/) -> ( f ` x ) e. x ) ) ) $=
    ( cvv wcel cv wfn c0 wne cfv wi wral wa wex cif wceq fneq2 raleq1 anbi12d
    exbidv 0ex elimel ac5 dedth ) BDEZCFZBGZAFZHIUHUFJUHEKZABLZMZCNUFUEBHOZGZUI
    AULLZMZCNBHBULPZUKUOCUPUGUMUJUNBULUFQUIABULRSTAULCBHDUAUBUCUD $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${ $d A f x y $. $d B f x y $. $d f ph $. $d ps y $.
    ac6sg.1 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( ~ ac6s with the first premise turned into a antecedent. $)
    ac6sg $p |- ( A e. V -> ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) ) $=
      ( cvv wcel wrex wral cv wf wa wex wi c0 cif wceq raleq1 feq2 anbi12d
      exbidv imbi12d 0ex elimel ac6s dedth ) EIJZADFKZCELZEFGMZNZBCELZOZGPZQUKC
      UJERSZLZURFUMNZBCURLZOZGPZQEREURTZULUSUQVCUKCEURUAVDUPVBGVDUNUTUOVAEURFUM
      UBBCEURUAUCUDUEABCDURFGERIUFUGHUHUI $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${ $d A x $. $d B x $. $d C x $. $d F x $. $d H x $. $d K x $.
    $( An injection is left-cancelable. $)
    njtlc $p |- ( ( F : B -1-1-> C /\ H : A --> B /\ K : A --> B ) ->
      ( ( F o. H ) = ( F o. K ) -> H = K ) ) $=
      ( vx wf1 wf w3a wceq cv ccom cfv wral wa wcel wfun fvco3 f1f ffun syl
      3ad2ant1 adantr 3simp2 pm3.27 syl3anc 3simp3 eqeq12d wi f1fveq biimpd
      3simp1 ffvelrn 3ad2antl2 3ad2antl3 jca sylanc sylbid r19.20dva anim2d
      wfn wb eqfnfv ffn fnfco ex 3syl imp 3adant3 3adant2 anim12i 3adant1
      3imtr4d ) BCDHZABEIZABFIZJZAAKZGLZDEMZNZVTDFMZNZKZGAOZPZVSVTENZVTFNZKZGAO
      ZPZWAWCKZEFKZVRWFWKVSVRWEWJGAVRVTAQZPZWEWHDNZWIDNZKZWJWPWBWQWDWRDRZVPWOWB
      WQKWPABVTDESVRWTWOVOVPWTVQVOBCDIZWTBCDTZBCDUAUBUCUDZVRVPWOVOVPVQUEUDVRWOU
      FZUGWTVQWOWDWRKWPABVTDFSXCVRVQWOVOVPVQUHUDXDUGUIVOWHBQZWIBQZPZWSWJUJWPVOX
      GPWSWJBCWHWIDUKULVRVOWOVOVPVQUMUDWPXEXFVPVOWOXEVQABVTEUNUOVQVOWOXFVPABVTF
      UNUPUQURUSUTVAWAAVBZWCAVBZWMWGVCVRGAAWAWCVDVOVPXHVQVOVPXHVOXADBVBZVPXHUJX
      BBCDVEZXJVPXHBADEVFVGVHVIVJVOVQXIVPVOVQXIVOXAXJVQXIUJXBXKXJVQXIBADFVFVGVH
      VIVKURVREAVBZFAVBZPZWNWLVCVPVQXNVOVPXLVQXMABEVEABFVEVLVMGAAEFVDUBVN $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${ $d A x y $.
  $( Restriction of the identity to a singleton. $)
  restidsing $p |- ( I |` { A } ) = ( { A } X. { A } ) $=
    ( vx vy cid csn cres cxp relres relxp cv cop wcel wa wbr df-br bicomi
    anbi1i pm3.27 wceq weq wi cvv visset ideqg biimpd ax-mp eqtr2 ex elsn
    syl6ibr syl syl5ib imp jca eqtr3 ideq equcom bitri sylibr sylbi com12
    pm3.26 impbii opelres opelxp 3bitr4i eqrelriv ) BCDAEZFZVHVHGZDVHHVHVHIBJZC
    JZKZDLZVKVHLZMZVOVLVHLZMZVMVILVMVJLVPVKVLDNZVOMZVRVNVSVOVSVNVKVLDOPQVTVRVTV
    OVQVSVORVSVOVQVSVKASZVQVOVSBCTZWAVQUAVLUBLZVSWBUACUCZWCVSWBVKVLUBUDUEUFWBWA
    VLASZVQWBWAWEVKVLAUGUHCAUIZUJUKBAUIZULUMUNVRVSVOVOVQVSVOWAVQVSUAWGVQWAVSVQW
    EWAVSUAWFWEWAVSWEWAMCBTZVSVLVKAUOVSWBWHVKVLWDUPBCUQURUSUHUTVAUTUMVOVQVBUNVC
    URVKVLDVHWDVDVKVLVHVHWDVEVFVG $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  $( The intersection of the identity function with a square cross
  product. $)
  residcp $p |- ( I i^i ( A X. A ) ) = ( I |` A ) $=
    ( cid cxp cin cres cvv wss ssid ssv ssxp mp2an sslin ax-mp df-res sseqtr4i
    resss scprefat2 ssini eqssi ) BAACZDZBAEZUABAFCZDZUBTUCGZUAUDGAAGAFGUEAHAIA
    AAFJKTUCBLMBANOUBBTBAPAQRS $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

   $( Image under a restricted class. $)
   imrescl $p |- ( A (_ B -> ( ( C |` B ) " A ) = ( C " A ) ) $=
     ( wss cres cima crn cin wceq df-ima a1i resres rneqi df-ss incom reseq2
     syl rneqd eqcomi 3eqtrd sylbi ) ABDZCBEZAFZUCAEZGZCBAHZEZGZCAFZUDUFIUBUCAJ
     KUFUIIUBUEUHCBALMKUBABHZAIZUIUJIABNULUICUKEZGCAEZGZUJULUHUMULUGUKIZUHUMIUP
     ULBAOKUGUKCPQRULUMUNUKACPRUOUJIULUJUOCAJSKTUAT $.
     $( [7-Sep-2009] $) $( [31-Aug-2009] $)


  ${ $d R x y $.
    $( Two ways of saying a relation is symmetric. $)
    twsymr $p |- ( Rel R -> ( R = `' R <-> A. x A. y ( x R y -> y R x ) ) ) $=
      ( wrel ccnv wss wa cv wbr wi wal wceq cnvsym biimpi a1d adantl com12
      dfrel2 sseq1 cnvss syl5cbi sylbir sylbi imp biimpri jca ex impbid eqss
      syl5bb ) CDZCCEZFZULCFZGZAHZBHZCIUQUPCIJBKAKZCULLUKUOURUOUKURUNUKURJUMUNU
      RUKUNURABCMZNOPQUKURUOUKURGUMUNUKURUMUKULEZCLZURUMJCRURVAUMURUNVAUMJUSVAU
      TULFUMUNUTCULSULCTUAUBQUCUDURUNUKUNURUSUEPUFUGUHCULUIUJ $.
      $( [7-Sep-2009] $) $( [31-Aug-2009] $)
  $}


  ${ $d R u y z $. $d R x y z $.
  $( Note: I suspect ` Rel R ` is useless ` . $)
  $( Projection of the first elements of the pairs of a class ` A ` . $)
  prj1 $p |- ( Rel R -> ( ( 1st |` ( V X. V ) ) " R ) =
    { x | E. y <. x , y >. e. R } ) $=
    ( vz vu wrel c1st cvv cxp cres cima cv cop wcel wex cab wbr wrex wi c2nd
    cfv wss df-rel ssel wceq wa elxp6 eleq1 wfun wfo fo1st fofun ax-mp funres
    visset funbrfv fvres eqtr2 ex opeq1 eleq1d biimpd a1d syl6 syl com4l com13
    syl6bi com23 imp com24 sylbi pm2.43i com14 fvex opeq2 cla4ev syl8
    r19.23aiv com12 co df-opr opelxp mpbir2an op1st 3eqtri wfn wb ssv fofn
    fnssresb mpbir fnotoprb mp3an mpbi df-br breq1 rcla4ev mpan2 19.23aiv
    impbid1 elima weq exbidv elab 3bitr4g eqrdv ) CFZDGHHIZJZCKZALZBLZMZCNZBOZA
    PZXHELZDLZXJQZECRZXSXMMZCNZBOZXSXKNXSXQNXHYAYDYAXHYDXTXHYDSECXRCNZXTXHXSXRT
    UAZMZCNZYDYEXTXHYHSSXHYEXTYEYHXHCXIUBZYEXTYEYHSSZSCUCYIYEXRXINZYJCXIXRUDYKY
    JYKXRXRGUAZYFMZUEZYLHNYFHNUFZUFZYKYJSXRHHUGYPYEXTYKYHYNYOYEXTYKYHSZSZSYNYEY
    OYRYNYEYMCNZYOYRSXRYMCUHXTYOYSYQXTXRXJUAZXSUEZYOYSYQSSXJUIZXTUUASGUIZUUBHHG
    UJZUUCUKHHGULUMXIGUNUMXRXSXJDUOZUPUMYKUUAYOYSYHYKYTYLUEZUUAYOYSYHSZSZSXRXIG
    UQUUFUUAYLXSUEZUUHUUFUUAUUIYTYLXSURUSUUIUUGYOUUIYSYHUUIYMYGCYLXSYFUTVAVBVCV
    DVEVFVEVGVHVIVJVKVLVMVDVLVNVMYCYHBYFXRTVOXMYFUEYBYGCXMYFXSVPVAVQVRVSVTYCYAB
    YCYBXSXJQZYAUUJYBXSMXJNZXSXMXJWAZXSUEZUUKUULYBXJUAZYBGUAZXSXSXMXJWBYBXINZUU
    NUUOUEUUPXSHNZXMHNZXSXMHHBUOZWCUUEUUSWDYBXIGUQUMXSXMUUEWEWFXJXIWGZUUQUURUUM
    UUKWHUUTXIHUBZXIWIGHWGZUUTUVAWHUUDUVBUKHHGWJUMHXIGWKUMWLUUEUUSHHXSXMXSXJUUE
    WMWNWOYBXSXJWPWLXTUUJEYBCXRYBXSXJWQWRWSWTXAEXSXJCUUEXBXPYDAXSUUEADXCZXOYCBU
    VCXNYBCXLXSXMUTVAXDXEXFXG $.
    $( [7-Sep-2009] $) $( [31-Aug-2009] $)
  $}

  ${ $d A x $.
   $( The image under ` 1st ` of a class with no pairs inside. $)
   imfstnrelc $p |- ( ( ( A i^i ( V X. V ) ) = (/) /\ A =/= (/) )
   -> ( 1st " A ) = { (/) } ) $=
     ( vx cvv cxp cin c0 wceq wne wa c1st cima cv cfv csn ciun wfun cdm wss
     wfo fo1st fofun ax-mp ssv wfn fofn fndm sseqtr4i pm3.2i a1i dfimafn2 syl
     wcel cuni wn wi wral wal disj biimpi df-ral sylib 19.21bi adantr imp
     dmsnn0 bicomi necon1bbii 0ss sseq1 mpbiri sylbi uni0b sylibr 1stval
     syl5eq sneqd iuneq2dv iunconst adantl 3eqtrd ) ACCDZEFGZAFHZIZJAKZBABLZJMZ
     NZOZBAFNZOZWJWDJPZAJQZRZIZWEWIGWOWDWLWNCCJSZWLTCCJUAUBACWMAUCJCUDZWMCGWPWQ
     TCCJUEUBCJUFUBUGUHUIBAJUJUKWDBAWHWJWDWFAULZIZWGFWSWFNQZUMZFWGWSWTWJRZXAFGW
     SWFWAULZUNZXBWDWRXDWBWRXDUOZWCWBXEBWBXDBAUPZXEBUQWBXFBAWAURUSXDBAUTVAVBVCV
     DXDWTFGZXBXCWTFXCWTFHWFVEVFVGXGXBFWJRWJVHWTFWJVIVJVKUKWTVLVMWFVNVOVPVQWCWK
     WJGWBBAWJVRVSVT $.
     $( [7-Sep-2009] $) $( [31-Aug-2009] $)
  $}


 ${ $d R x y $.
  $( Projection of the first elements of the pairs of a relation ` R ` . $)
  prj1b $p |- ( Rel R -> ( 1st " R ) = { x | E. y <. x , y >. e. R } ) $=
    ( wrel c1st cima cvv cxp cres cv cop wcel wex cab wss wceq df-rel imrescl
    eqcomd sylbi prj1 eqtrd ) CDZECFZEGGHZICFZAJBJKCLBMANUCCUEOZUDUFPCQUGUFUDCU
    EERSTABCUAUB $.
    $( [7-Sep-2009] $) $( [31-Aug-2009] $)
  $}

  ${ $d R x y $.
  $( The projection of the first elements of the pairs of a relation ` R `
     is its domain. $)
  prjdmn $p |- ( Rel R -> ( 1st " R ) = dom R ) $=
    ( vx vy wrel c1st cima cv cop wcel wex cab cdm prj1b dfdm3 syl6eqr ) ADEAFB
    GCGHAICJBKALBCAMBCANO $.
    $( [6-Oct-2009] $) $( [5-Oct-2009] $)
  $}

  $( Projection of a cross product. $)
  prjcp1 $p |- ( B =/= (/) -> ( 1st " ( A X. B ) ) = A ) $=
    ( c0 wne c1st cxp cima cdm wrel wceq wi relxp prjdmn a1d ax-mp dmxp eqtrd
    ) BCDZEABFZGZSHZASIZRTUAJZKABLUBUCRSMNOABPQ $.
    $( [6-Oct-2009] $) $( [5-Oct-2009] $)

  $( Equality theorem for cross product. $)
  xpeq12 $p |- ( ( A = B /\ C = D ) -> ( A X. C ) = ( B X. D ) ) $=
    ( wceq cxp xpeq1 xpeq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [7-Sep-2009] $) $( [31-Aug-2009] $)

  $( Equality theorem for class difference. $)
  difeq12 $p |- ( ( A = B /\ C = D ) -> ( A \ C ) = ( B \ D ) ) $=
    ( wceq cdif difeq1 difeq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [7-Sep-2009] $) $( [31-Aug-2009] $)

  ${
    xpeq12i.1 $e |- A = B $.
    xpeq12i.2 $e |- C = D $.
    $( Equality theorem for cross product. $)
    xpeq12i $p |- ( A X. C ) = ( B X. D ) $=
      ( wceq cxp xpeq12 mp2an ) ABGCDGACHBDHGEFABCDIJ $.
      $( [7-Sep-2009] $) $( [31-Aug-2009] $)
  $}

  ${ $d A v w x y z $.  $d ph x $.  $d ta v w y z $.
    eloi.1 $e |- ( y = ( 1st ` ( 1st ` A ) ) -> ( ph <-> ps ) ) $.
    eloi.2 $e |- ( z = ( 2nd ` ( 1st ` A ) ) -> ( ps <-> ch ) ) $.
    eloi.3 $e |- ( v = ( 1st ` ( 2nd ` A ) ) -> ( ch <-> th ) ) $.
    eloi.4 $e |- ( w = ( 2nd ` ( 2nd ` A ) ) -> ( th <-> ta ) ) $.
    $( A consequence of membership in a class abstraction whose elements
       belong to ` ( ( V X. V ) X. ( V X. V ) ) `  using ordered pair
       extractors. (Used by category theory). $)
    eloi $p |- ( A e. { x | E. y E. z E. v E. w
      ( x = <. <. y , z >. , <. v , w >. >. /\ ph ) } -> ta ) $=
      ( cv cop wceq wa wex cab wcel c1st cfv c2nd eqeq1 anbi1d 4exbidv elabg
      ibi cvv cxp id opex opelxpi mp2an syl6eqel wrel relxp 1st2nd mpan syl
      fveq2 op1st syl6eq visset op2nd opeq12d eqtrd adantr 19.23aivv eleq1 w3a
      wb fvex 3pm3.2i elo syl6bb biimpcd mpd ) KFPZGPZHPZQZJPZIPZQZQZRZASZITJTH
      TGTZFUAZUBZKKUCUDZUCUDZWNUEUDZQZKUEUDZUCUDZWRUEUDZQZQZRZEWMKWHRZASZITJTZH
      TGTZXCWMXGWKXGFKWLWAKRZWJXEGHJIXHWIXDAWAKWHUFUGUHUIUJXFXCGHXEXCJIXDXCAXDK
      WNWRQZXBXDKUKUKULZUBZKXIRZXDKWHXJXDUMWDUKUBWGUKUBWHXJUBWBWCUNZWEWFUNZWDWG
      UKUKUOUPUQXJURZXKXLUKUKUSZKXJUTVAVBXDWNWQWRXAXDWNXJUBZWNWQRZXDWNWDXJXDWNW
      HUCUDWDKWHUCVCWDWGXMVDVEWBUKUBWCUKUBWDXJUBGVFHVFWBWCUKUKUOUPUQXOXQXRXPWNX
      JUTVAVBXDWRXJUBZWRXARZXDWRWGXJXDWRWHUEUDWGKWHUEVCWDWGXMXNVGVEWEUKUBWFUKUB
      WGXJUBJVFIVFWEWFUKUKUOUPUQXOXSXTXPWRXJUTVAVBVHVIVJVKVKVBXCWMEXCWMXBWLUBZE
      KXBWLVLWOUKUBZWPUKUBZWSUKUBZVMWTUKUBYAEVNYBYCYDWNUCVOWNUEVOWRUCVOVPWRUEVO
      ABCDEFGHIJWOWPWSWTUKUKUKUKLMNOVQUPVRVSVT $.
      $( [24-Sep-2007] $) $( [24-Sep-2007] $)
  $}

  $c t+ $.
  $c t* $.

  ctcl $a class t+ $. $( Extend class notation with transitive closure. $)
  crtcl $a class t* $. $( Extend class notation with reflexive transitive
    closure. $)

  ${ $d x y z $.
    $( Transitive closure of a relation. Experimental. Maybe correct.
       Maybe not. $)
    trcl-df $a |- t+ = { <. x , y >. |
      y = |^| { z | ( x (_ z /\ ( z o. z ) (_ z ) } } $.

    $( Reflexive-transitive closure of a relation. Experimental. Maybe correct.
       Maybe not. $)
    rtrcl-df $a |- t* = { <. x , y >. |
      y = |^| { z | ( ( I |` ( dom x u. ran x ) ) (_ z /\ x (_ z
      /\ ( z o. z ) (_ z ) } } $.
  $}


  $( The double union of an intersection is a part of the intersections of the
     unions. $)
  uuniin $p |- U. U. ( A i^i B ) (_ ( U. U. A i^i U. U. B ) $=
    ( cin cuni wss uniin uniss sstr2 mpi syl ax-mp ) ABCDZADZBDZCZEZLDZMDNDCZEZ
    ABFPQODZEZSLOGUATRESMNFQTRHIJK $.
    $( [24-Jan-2010] $) $( [24-Jan-2010] $)

  $( A finite set is equal to its subset if they are equinumerous. $)
  sfseqeq $p |- ( ( B e. Fin /\ A (_ B /\ A ~~ B ) -> A = B ) $=
    ( cen wbr wss cfn wcel wceq wa wne wn wpss pssinf expcom df-pss syl5ibr
    expdimp necon4ad 3impia 3com13 ) ABCDZABEZBFGZABHZUAUBUCUDUAUBIUCABUAUBABJZ
    UCKZUAABLZUFUBUEIUGUAUFABMNABOPQRST $.
    $( [6-Sep-2008] $) $( [11-Aug-2008] $)

  $( Conditions to have a set of two elements. To be suppressed.
     See ~ unpde2eg22 instead. $)
  unpde2eg2 $p |- ( ( A e. C /\ B e. D /\ A =/= B ) -> { A , B } ~~ 2o ) $=
    ( wcel wne cpr c2o cen wbr csn cin c0 wceq wi c1o wa cun pm54.43 df-pr
    breq1i syl6bbr biimpd ensn1g syl2an ex disjsn2 syl7 3imp ) ACEZBDEZABFZABGZ
    HIJZUJUKAKZBKZLMNZUNULUJUKUQUNOZUOPIJZUPPIJZURUJUKUSUTQZUQUNVAUQUOUPRZHIJUN
    UOUPSUMVBHIABTUAUBUCACUDBDUDUEUFABUGUHUI $.
    $( [1-Sep-2008] $) $( [17-Aug-2008] $)

  $( If an unordered pair has two elements they are different. $)
  unpde2eg22 $p |- ( ( A e. C /\ B e. D ) -> ( { A , B } ~~ 2o <->  A =/= B ) ) $=
    ( wcel wa cpr c2o cen wbr wne wceq wn wi c1o boe cvv prex eqeng ax-mp entr
    csdm 1sdom2 sdomnen con0 1on elisseti ensym ex syl mtoi a1d com12 a1dd
    com23 imp pm2.43a preq2 eqcoms syl5 necon2ad unpde2eg2 3expia impbid ) ACEZ
    BDEZFZABGZHIJZABKZVGVIABVGVHAAGZLZVIMZABLVLVGVMVEVFVLVGVMNZNVEVLVFVNVEVKOIJ
    ZVLVFVNNNACPVOVLVNVFVLVOVNVLVHVKIJZVOVNNVHQEVLVPNABRVHVKQSTVPVOVNVPVOFVHOIJ
    ZVNVHVKOUAVQVMVGVQVIOHIJZOHUBJVRMUCOHUDTVQOVHIJZVIVRNVHOOUEUFUGUHVSVIVROVHH
    UAUIUJUKULUJUIUJUMUNUJUOUPUQVLBABAAURUSUTVAVEVFVJVIABCDVBVCVD $.
    $( [18-Feb-2010] $) $( [14-Feb-2010] $)

  $( Building a set with two elements. $)
  set2elt $p |- ( ( C ~~ 2o /\ A e. C /\ B e. C )
     -> ( A =/= B -> C = { A , B } ) ) $=
    ( c2o cen wbr wcel wne cpr wceq wi wa wss prssg w3a cfn com 2onn ssid
    ssnnfi mp2an wb enfi mpan mpbiri entr prex ensym sfseqeq eqcomd 3exp com3r
    syl ex ensymg ax-mp syl5com com13 mpcom unpde2eg2 3expia com24 sylbid
    pm2.43i com12 3impib ) CDEFZACGZBCGZABHZCABIZJZKZVHVILZVGVMVNVGVMKZVNVNVKCM
    ZVOABCCCNVNVJVGVPVLVHVIVJVGVPVLKZKVGVKDEFZVQVHVIVJOCPGZVGVRVQKVGVSDPGZDQGZD
    DMVTRDSDDTUAWAVGVSVTUBRCDQUCUDUEVRVGVSVQVGDVKEFZVSVQKZVRVGWBWCVGWBLCVKEFZWC
    CDVKUFWDVKCEFZWCCVKABUGUHVSVPWEVLVSVPWEVLVSVPWEOVKCVKCUIUJUKULUMUMUNWAVRWBK
    RVKDQUOUPUQURUSABCCUTUQVAVBVCVDVEVF $.
    $( [6-Sep-2008] $) $( [11-Aug-2008] $)

  $( Building a set with only one element. $)
  setwoe $p |- ( ( A e. B /\ B ~~ 1o ) -> B = { A } ) $=
    ( wcel c1o cen wbr wa csn cfn wss wceq sfseqeq com 1onn ssid ssnnfi mp2an
    wb enfi mpan mpbiri adantl snssi adantr entr ensn1g wi ensymg ax-mp syl2an
    syl3anc eqcomd ) ABCZBDEFZGZAHZBBICZUPBJZUPBEFZUPBKUOUPBLUNUQUMUNUQDICZDMCZ
    DDJUTNDODDPQVAUNUQUTRNBDMSTUAUBUMURUNABUCUDUPDEFDBEFZUSUMUNUPDBUEABUFVAUNVB
    UGNBDMUHUIUJUKUL $.
    $( [6-Sep-2008] $) $( [18-Aug-2008] $)

  $( The range of a square cross product. $)
  rnxpid $p |- ran ( A X. A ) = A $=
    ( cxp crn wceq c0 rn0 xpeq2 xp0 syl6eq rneqd id 3eqtr4a rnxp pm2.61ine ) AA
    BZCZADAEAEDZECEPAFQOEQOAEBEAEAGAHIJQKLAAMN $.
    $( [18-May-2010] $) $( [17-May-2010] $)

  ${
    $d t u v A $. $d t u v B $. $d t u v C $.
    $( Subset relation for cross product.
       (Contributed by Jeff Hankins, 14-Aug-2009.) $)
    xpss1 $p |- ( A (_ B -> ( A X. C ) (_ ( B X. C ) ) $=
      ( vt vu vv wss cxp cv cop wceq wcel wa wex opelxpi ssel2 adantrr simprr
      sylanc adantrl wb eleq1 ad2antrl mpbird ex 19.23advv elxp syl5ib ssrdv )
      ABGZDACHZBCHZUJDIZEIZFIZJZKZUNALZUOCLZMZMZFNENUMULLZUMUKLUJVAVBEFUJVAVBUJ
      VAMVBUPULLZUJUTVCUQUNBLZUSVCUJUTMUNUOBCOUJURVDUSABUNPQUJURUSRSTUQVBVCUAUJ
      UTUMUPULUBUCUDUEUFEFUMACUGUHUI $.
      $( [30-Aug-2009] $) $( [14-Aug-2009] $)
  $}

  ${
    $d t u v A $. $d t u v B $. $d t u v C $.
    $( Subset relation for cross product.
       (Contributed by Jeff Hankins, 14-Aug-2009.) $)
    xpss2 $p |- ( A (_ B -> ( C X. A ) (_ ( C X. B ) ) $=
      ( vt vu vv wss cxp cv cop wceq wcel wa wex opelxpi simprl ssel2 adantrl
      sylanc wb eleq1 ad2antrl mpbird ex 19.23advv elxp syl5ib ssrdv ) ABGZDCAH
      ZCBHZUIDIZEIZFIZJZKZUMCLZUNALZMZMZFNENULUKLZULUJLUIUTVAEFUIUTVAUIUTMVAUOU
      KLZUIUSVBUPUQUNBLZVBUIUSMUMUNCBOUIUQURPUIURVCUQABUNQRSRUPVAVBTUIUSULUOUKU
      AUBUCUDUEEFULCAUFUGUH $.
      $( [30-Aug-2009] $) $( [14-Aug-2009] $)
  $}

  $( Subset relation for cross product.
     (Contributed by Jeff Hankins, 14-Aug-2009.) $)
  xpss12 $p |- ( ( A (_ B /\ C (_ D ) -> ( A X. C ) (_ ( B X. D ) ) $=
    ( wss cxp xpss1 xpss2 sylan9ss ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [30-Aug-2009] $) $( [14-Aug-2009] $)

  ${
    $d A b c $. $d B b c $. $d C b c $.
    $( Location of the first element of a Cartesian product.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    xp1st $p |- ( A e. ( B X. C ) -> ( 1st ` A ) e. B ) $=
      ( vb vc cxp wcel cv cop wceq wa wex c1st cfv elxp fveq2 visset op1st
      syl6eq eleq1d biimpar adantrr 19.23aivv sylbi ) ABCFGADHZEHZIZJZUEBGZUFCG
      ZKKZELDLAMNZBGZDEABCOUKUMDEUHUIUMUJUHUMUIUHULUEBUHULUGMNUEAUGMPUEUFDQRSTU
      AUBUCUD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A b c $. $d B b c $. $d C b c $.
    $( Location of the second element of a Cartesian product.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    xp2nd $p |- ( A e. ( B X. C ) -> ( 2nd ` A ) e. C ) $=
      ( vb vc cxp wcel cv cop wceq wa wex c2nd cfv elxp fveq2 visset op2nd
      syl6eq eleq1d biimpar adantrl 19.23aivv sylbi ) ABCFGADHZEHZIZJZUEBGZUFCG
      ZKKZELDLAMNZCGZDEABCOUKUMDEUHUJUMUIUHUMUJUHULUFCUHULUGMNUFAUGMPUEUFDQEQRS
      TUAUBUCUD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y z $. $d B u v w x y z $.
    $d ph u v w $. $d ps u v $. $d ch u v $.
    oprabopabf.1 $e |- ( ps -> A. x ps ) $.
    oprabopabf.2 $e |- ( ch -> A. y ch ) $.
    oprabopabf.3 $e |- ( x = ( 1st ` w ) -> ( ph <-> ps ) ) $.
    oprabopabf.4 $e |- ( y = ( 2nd ` w ) -> ( ps <-> ch ) ) $.
    $( Convert between operator and function class abstractions using
       ` 1st ` and ` 2nd ` . (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    oprabopabf $p |- { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ ph ) }
                          = { <. w , z >. | ( w e. ( A X. B ) /\ ch ) } $=
      ( vu vv cv cop wceq wcel wa wex cab cxp copab2 copab ax-17 c2nd cfv wal
      wi fvex isseti hbal hbim albidv imbi12d mpbii 19.23ai ax-mp hban hbex
      opex opeq1 eqeq2d eleq1 c1st wb fveq2 visset op1st syl6req syl op2nd
      bitr2d anbi12d cla4ev opelxpi sylanr1 wrel relxp 1st2nd mpan opeq1d
      biimpac adantrr xp1st xp2nd jca ad2antrl simprr cvv opeq2 anbi2d cla4egf
      anbi1d 19.22i excom sylib sylan32c 19.23aiv impbii exbii bitri 3bitr4i
      abbii df-oprab df-opab 3eqtr4i ) NPZDPZEPZQZFPZQZRZXJHSZXKISZTZATZTZFUAEU
      AZDUAZNUBXIGPZXMQZRZYCHIUCZSZCTZTZFUAGUAZNUBXSDEFUDYHGFUEYBYJNXTEUAZDUAZF
      UAZYIGUAZFUAYBYJYLYNFYLYNYKYNDYIDGYEYHDYEDUFYGCDYGDUFXKYCUGUHZRZEUACCDUIZ
      UJZEYOYCUGUKZULYPYRECYQEKCEDKUMUNYPBBDUIZUJYRJYPBCYTYQMYPBCDMUOUPUQURUSUT
      UTVAXTYNEYIEGYEYHEYEEUFYGCEYGEUFKUTUTVAXOXLYFSZAYNXRYIXOUUAATZTGXLXJXKVBY
      CXLRZYEXOYHUUBUUCYDXNXIYCXLXMVCVDUUCYGUUACAYCXLYFVEUUCABCUUCXJYCVFUHZRZAB
      VGUUCUUDXLVFUHXJYCXLVFVHXJXKDVIZVJVKLVLUUCYPBCVGUUCYOXLUGUHXKYCXLUGVHXJXK
      UUFEVIVMVKMVLVNVOVOVPXJXKHIVQVRURURYIYLGXIUUDYOQZXMQZRZUUDHSZYOISZTZCYLYI
      YEYGUUICYGYEUUIYGYDUUHXIYGYCUUGXMYFVSYGYCUUGRHIVTYCYFWAWBWCVDWDWEYGUULYEC
      YGUUJUUKYCHIWFYCHIWGWHWIYEYGCWJUUIUULCTZTZXTDUAZEUAZYLUUNXIUUDXKQZXMQZRZU
      UJXQTZBTZTZEUAZUUPYOWKSUUNUVCUJYSUVBUUNEOYOWKOPZYOSEUFUUIUUMEUUIEUFUULCEU
      ULEUFKUTUTYPUUSUUIUVAUUMYPUURUUHXIYPUUQUUGXMXKYOUUDWLWCVDYPUUTUULBCYPXQUU
      KUUJXKYOIVEWMMVOVOWNUSUVBUUOEUUDWKSUVBUUOUJYCVFUKXTUVBDOUUDWKUVDUUDSDUFUU
      SUVADUUSDUFUUTBDUUTDUFJUTUTUUEXOUUSXSUVAUUEXNUURXIUUEXLUUQXMXJUUDXKVCWCVD
      UUEXRUUTABUUEXPUUJXQXJUUDHVEWOLVOVOWNUSWPVLXTEDWQWRWSWTXAXBYBYKFUAZDUAYMY
      AUVEDXTEFWQXBYKDFWQXCYIGFWQXDXEXSDEFNXFYHGFNXGXH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A w x y z $. $d B w x y z $. $d ph w $. $d ps x $. $d ch y $.
    oprabopab.1 $e |- ( x = ( 1st ` w ) -> ( ph <-> ps ) ) $.
    oprabopab.2 $e |- ( y = ( 2nd ` w ) -> ( ps <-> ch ) ) $.
    $( Convert between operator and function class abstractions using
       ` 1st ` and ` 2nd ` . (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    oprabopab $p |- { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ ph ) }
                          = { <. w , z >. | ( w e. ( A X. B ) /\ ch ) } $=
      ( ax-17 oprabopabf ) ABCDEFGHIBDLCELJKM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y z $. $d B u v w x y z $. $d C u v w z $.
    $d D u v w x y z $. $d F u v $. $d G u v z $. $d H u v w x y z $.
    oprabco.1 $e |- ( ( x e. A /\ y e. B ) -> C e. D ) $.
    oprabco.2 $e |- F = { <. <. x , y >. , z >. |
                                ( ( x e. A /\ y e. B ) /\ z = C ) } $.
    oprabco.3 $e |- G = { <. <. x , y >. , w >. |
                                ( ( x e. A /\ y e. B ) /\ w = ( H ` C ) ) } $.
    $( Composition of a function with an operator abstraction.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    oprabco $p |- ( H Fn D -> G = ( H o. F ) ) $=
      ( vu vv wfn ccom wceq cxp cv cfv wral wa eqidd wcel c2nd c1st csb rgen2
      ax-17 fvex hbcsb1 hbel hbral csbeq1a eleq1d ralbidv rcla4 sylan9 mpi
      xp1st xp2nd sylanc fveq2 csbeq1d cvv csbeq2dv mpan2 eqtrd copab2 copab
      hbeq eqeq2d oprabopabf eqtri fvopab4g mpdan adantl fveq2d csbfv2g ax-mp
      csbeq2i syl6eqr wfun cdm fvco fnfun adantr wf foprab ffn a1i fdmi eleq2i
      biimpri syl3anc csbex 3eqtr4rd r19.21aiva jca wb eqfnfv fnoprab2 crn wss
      frn fnco mp3an23 sylancr mpbird ) KHQZJKIRZSZEFTZXOSZOUAZJUBZXQXMUBZSZOXO
      UCZUDZXLXPYAXLXOUEXLXTOXOXLXQXOUFZUDZXQIUBZKUBZBXQUGUBZAXQUHUBZGKUBZUIZUI
      ZXSXRYDYFBYGAYHGUIZUIZKUBZYKYDYEYMKYCYEYMSZXLYCYMHUFZYOYHEUFZYGFUFZYPYCYQ
      YRUDGHUFZBFUCZAEUCZYPYSABEFLUJYQUUAYLHUFZBFUCZYRYPYTUUCAYHEUUBABFBUAZFUFZ
      AUKAPPYLHAPYHGXQUHULZPUAZYHUFAUKUMUUGHUFZAUKUNUOAUAZYHSZYSUUBBFUUJGYLHAYH
      GUPUQURUSUUBYPBYGFBPPYMHBPYGYLXQUGULZUUGYGUFBUKUMUUHBUKUNUUDYGSYLYMHBYGYL
      UPUQUSUTVAXQEFVBXQEFVCVDPCXQBUUGUGUBZAUUGUHUBZGUIZUIZYMXOHIUUGXQSZUUOBYGU
      UNUIZYMUUPBUULYGUUNUUGXQUGVEZVFUUPYGVGUFZUUQYMSUUKUUPBYGUUNYLVGUUPAUUMYHG
      UUGXQUHVEZVFVHVIVJIUUIEUFUUEUDZCUAZGSZUDABCVKUUGXOUFZUVBUUOSZUDPCVLMUVCUV
      BUUNSUVEABCPEFAOOUVBUUNXQUVBUFZAUKAOUUMGUUGUHULZXQUUMUFAUKZUMVMBOOUVBUUOU
      VFBUKBOUULUUNUUGUGULZXQUULUFBUKZUMVMUUIUUMSZGUUNUVBAUUMGUPVNUUDUULSZUUNUU
      OUVBBUULUUNUPVNVOVPVQVRVSVTYKBYGYLKUBZUIZYNUUSYKUVNSUUKBYGYJUVMVGYHVGUFYJ
      UVMSUUFAYHGVGKWAWBWCWBUUSUVNYNSUUKBYGYLVGKWAWBVPWDKWEZIWEZXQIWFZUFZXSYFSY
      DXQKIWGXLUVOYCHKWHWIUVPYDIXOQZUVPXOHIWJZUVSABCEFGHIMLWKZXOHIWLWBZXOIWHWBW
      MYCUVRXLUVRYCUVQXOXQXOHIUWAWNWOWPVSWQYCXRYKSZXLYCYKVGUFUWCBYGYJUUKAYHYIUU
      FGKULZWRWRPDXQBUULAUUMYIUIZUIZYKXOVGJUUPUWFBYGUWEUIZYKUUPBUULYGUWEUURVFUU
      PUUSUWGYKSUUKUUPBYGUWEYJVGUUPAUUMYHYIUUTVFVHVIVJJUVADUAZYISZUDABDVKUVDUWH
      UWFSZUDPDVLNUWIUWHUWESUWJABDPEFAOOUWHUWEXQUWHUFZAUKAOUUMYIUVGUVHUMVMBOOUW
      HUWFUWKBUKBOUULUWEUVIUVJUMVMUVKYIUWEUWHAUUMYIUPVNUVLUWEUWFUWHBUULUWEUPVNV
      OVPVQVIVSWSWTXAJXOQXMXOQZXNYBXBXLOXOXOJXMXCABDEFYIJUWDNXDXLUVSIXEHXFZUWLU
      WBUVTUWMUWAXOHIXGWBHXOKIXHXIXJXK $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z w $. $d B x y z w $. $d C z w $. $d D z w $.
    $d R x y z w $. $d S x y z w $. $d M x y z w $. $d F w $. $d G z $.
    oprab2co.1 $e |- ( ( x e. A /\ y e. B ) -> C e. R ) $.
    oprab2co.2 $e |- ( ( x e. A /\ y e. B ) -> D e. S ) $.
    oprab2co.3 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
                                                  /\ z = <. C , D >. ) } $.
    oprab2co.4 $e |- G = { <. <. x , y >. , w >. | ( ( x e. A /\ y e. B )
                                                  /\ w = ( C M D ) ) } $.
    $( Composition of operator abstractions.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    oprab2co $p |- ( M Fn ( R X. S ) -> G = ( M o. F ) ) $=
      ( cop cxp wcel cv wa opelxpi sylanc co wceq copab2 cfv df-opr eqeq2i
      anbi2i oprabbii eqtri oprabco ) ABCDEFGHRZIJSZKLMGITHJTUOUPTAUAETBUAFTUBZ
      GHIJUCNOUDPLUQDUAZGHMUEZUFZUBZABDUGUQURUOMUHZUFZUBZABDUGQVAVDABDUTVCUQUSV
      BURGHMUIUJUKULUMUN $.
      $( [2-Sep-2009] $)
  $}

  ${
    $( Membership in the preimage of a set under a function.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    elpreima $p |- ( F Fn A -> ( B e. ( `' F " C )
                                <-> ( B e. A /\ ( F ` B ) e. C ) ) ) $=
      ( wfn ccnv cima wcel cfv wa cdm fndm eleq2d cnvimass sseli syl5bi wfun
      fvimacnvi fnfun sylan ex jcad wb fvimacnv funfni biimpd expimpd impbid )
      DAEZBDFCGZHZBAHZBDICHZJUIUKULUMUIBDKZHULUKUIUNABADLMUJUNBDCNOPUIUKUMDQUKU
      MUIBCDRADSTUAUBUIULUMUKUIULJUMUKUMUKUCABDBCDUDUEUFUGUH $.
      $( [2-Sep-2009] $)
  $}

  ${
    opreq123i.1 $e |- A = C $.
    opreq123i.2 $e |- B = D $.
    opreq123i.3 $e |- F = G $.
    $( Equality inference for operation value. $)
    opreq123i $p |- ( A F B ) = ( C G D ) $=
      ( co opreq12i opreqi eqtri ) ABEJCDEJCDFJACBDEGHKEFCDILM $.
      $( [11-Jul-2010] $) $( [11-Jul-2010] $)
  $}


