$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Operation properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Com1 $.

  $( Extend class notation with a class that adds commutativity to semi-groups,
     monoids and so on. $)
  ccm1 $a class Com1 $.

  ${
    $d g x y $.
    $( A device to add commutativity to magmas, semi-groups, monoids and so
       on.  A commutative group is composed of 5 properties (internal
       operation, commutativity, associativity, existence of a neutral element
       and an inverse).  If we switch on or off those four properties we get 32
       structures.  Instead of giving a name to those 32 structures, I suggest
       we use intersected classes and speak of ` ( Magma i^i Com1 ) ` or
       ` ( Magma i^i ExId ) ` . $)
    df-com1 $a |- Com1 = { g | A. x e. dom dom g A. y e. dom dom g
      ( x g y ) = ( y g x ) } $.
  $}

  ${
    iscom.1 $e |- X = dom dom G $.
    ${
      $d G a x y $.  $d X a $.
      $( The predicate "is a commutative operation". $)
      iscom $p |- ( G e. A -> ( G e. Com1 <-> A. x e. X A. y e. X
      ( x G y ) = ( y G x ) ) ) $=
        ( va cv co wceq cdm wral ccm1 wcel dmeq eqcomd dmeqd syl5req eleq2d
        opreq eqeq12d imbi12d ralbidv2 df-com1 elab2g ) AHZBHZGHZIZUGUFUHIZJZBU
        HKZKZLZAUMLUFUGDIZUGUFDIZJZBELZAELGDMCUHDJZUNURAUMEUSUFUMNUFENUNURUSUME
        UFUSDKZKUMEUSUTULUSULUTUHDOPQFRZSUSUKUQBUMEUSUGUMNUGENUKUQUSUMEUGVASUSU
        IUOUJUPUFUGUHDTUGUFUHDTUAUBUCUBUCABGUDUE $.
        $( [5-Sep-2010] $)
    $}

    ${
      $d A x y $.  $d B y $.  $d G x y $.  $d X x y $.
      $( The predicate "is a commutative operation". $)
      iscomb $p |- ( ( G e. Com1 /\ A e. X /\ B e. X )
       -> ( A G B ) = ( B G A ) ) $=
        ( vx vy ccm1 wcel co wceq wi cv wral iscom opreq1 opreq2 eqeq12d
        rcla42v ex com3r syl6bi pm2.43i 3imp ) CHIZADIZBDIZABCJZBACJZKZUEUFUGUJ
        LLZUEUEFMZGMZCJZUMULCJZKZGDNFDNZUKFGHCDEOUFUGUQUJUFUGUQUJLUPUJAUMCJZUMA
        CJZKFGABDDULAKUNURUOUSULAUMCPULAUMCQRUMBKURUHUSUIUMBACQUMBACPRSTUAUBUCU
        D $.
        $( [14-Sep-2010] $)
    $}
  $}

  $c Free $.

  $c star $.

  $( Extend class notation to include the class of free operations. $)
  cfree $a class Free $.

  $( Extend class notation to include the star operator. $)
  cstar $a class star $.


  $( Meaningful if g is a semigroup and x a part of the base set. $)
  df-star $a |- star = { <. <. g , x >. , y >. | y = { a e. dom dom g |
     E. n e. NN E. f e. ( x ^m ( 1 ... n ) )
        a = prod3_ k e. ( 1 ... n ) g ( f ` k ) } } $.

  ${
    $d g b x n f k $.
    $( Definition of a free operation: a subset of the underlying set of the
       operation is a basis for every element of the underlying set (that is
       every element can be decomposed into a sequence of elements of the basis
       in a unique way).  Meaningful if ` g ` is a magma, a semi-group, a
       monoid.  Experimental. $)
    df-free $a |- Free = { g | E. b e. ~P dom dom g A. x e. dom dom g
      E! n e. NN E! f e. ( b ^m ( 1 ... n ) ) x =
                    prod3_ k e. ( 1 ... n ) g ( f ` k ) } $.
  $}

