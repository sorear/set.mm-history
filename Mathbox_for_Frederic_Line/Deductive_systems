$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Deductive systems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Ded $. $( The class of all deductive systems. $)

  $( Extend class notation with the class of deductive systems. $)
  cded $a class Ded $.

  ${
    $d x d c j r a f g $.
    $( Definition of a deductive system.  Lambeck and Scott.  Introduction to
       higher order categorical logic. p. 47. 1986.  Unformally we can say a
       deductive system is a directed multi graph where for each object a
       specific morphism called identity of the object exists and where for
       some pairs of morphisms the composite exists.  Deductive system are
       named so because morphisms may be interpreted as logical deductions,
       objects as sets of formulas and compositions as inferences. $)
    df-ded $a |- Ded = { x | E. d E. c E. j E. r
    ( x = <. <. d , c >. , <. j , r >. >. /\ ( (
      <. <. d , c >. , <. j , r >. >. e. Alg /\
      A. a e. dom j
         ( ( d ` ( j ` a ) ) = a /\ ( c ` ( j ` a ) ) = a ) /\
      A. f e. dom d A. g e. dom d ( <. g , f >. e. dom r
        <-> ( d ` g ) = ( c ` f ) ) ) /\ (
      A. f e. dom d A. g e. dom d
         ( ( d ` g ) = ( c ` f ) -> ( d ` ( g r f ) ) = ( d ` f ) ) /\
      A. f e. dom d A. g e. dom d
         ( ( d ` g ) = ( c ` f ) -> ( c ` ( g r f ) ) = ( c ` g ) ) ) ) ) } $.
  $}

  ${
    $d C a c d f g j r $.  $d D a c d f g j r $.  $d J a c d j r $.
    $d M c d j r $.  $d O c d j r $.  $d R c d f g j r $.
    $d a c d f g j r x $.
    isded.1 $e |- M = dom D $.
    isded.2 $e |- O = dom J $.
    $( The predicate "is a deductive system".  (Contributed by FL,
       24-Oct-2007.) $)
    isded $p |- ( ( ( D e. A /\ C e. B /\ J e. F ) /\ R e. G )
      -> ( <. <. D , C >. , <. J , R >. >. e. Ded
        <-> ( ( <. <. D , C >. , <. J , R >. >. e. Alg /\
      A. a e. O
         ( ( D ` ( J ` a ) ) = a /\ ( C ` ( J ` a ) ) = a ) /\
      A. f e. M A. g e. M ( <. g , f >. e. dom R
        <-> ( D ` g ) = ( C ` f ) ) ) /\ (
      A. f e. M A. g e. M
         ( ( D ` g ) = ( C ` f ) -> ( D ` ( g R f ) ) = ( D ` f ) ) /\
      A. f e. M A. g e. M
         ( ( D ` g ) = ( C ` f ) -> ( C ` ( g R f ) ) = ( C ` g ) ) ) ) ) ) $=
      ( wcel wceq cfv wa wral vx vd vc vj vr cop cded cv calg cdm wb w3a co wex
      wi cab df-ded eleq2i opeq1 opeq1d eleq1d fveq1 eqeq1d anbi1d ralbidv dmeq
      syl6eqr eleq2d bibi2d imbi12d ralbidv2 eqeq12d imbi1d opeq2 anbi2d eqeq2d
      3anbi123d anbi12d 2ralbidv opeq2d fveq2d 3anbi12d bibi1d opreq imbi2d elo
      3anbi13d syl5bb ) DCUFZJEUFZUFZUGPWKUAUHUBUHZUCUHZUFZUDUHZUEUHZUFZUFZQWRU
      IPZMUHZWORZWLRZWTQZXAWMRZWTQZSZMWOUJZTZGUHZFUHZUFZWPUJZPZXIWLRZXJWMRZQZUK
      ZGWLUJZTZFXRTZULZXPXIXJWPUMZWLRZXJWLRZQZUOZGXRTZFXRTZXPYBWMRZXIWMRZQZUOZG
      XRTZFXRTZSZSZSUEUNUDUNUCUNUBUNUAUPZPDAPCBPJHPULEIPSWKUIPZWTJRZDRZWTQZYSCR
      ZWTQZSZMLTZXKEUJZPZXIDRZXJCRZQZUKZGKTFKTZULZUUJXIXJEUMZDRZXJDRZQZUOZGKTFK
      TZUUJUUNCRZXICRZQZUOZGKTFKTZSZSZUGYQWKUAFGUDUEMUCUBUQURYPDWMUFZWQUFZUIPZX
      ADRZWTQZXESZMXGTZXMUUHXOQZUKZGKTZFKTZULZUVNYBDRZUUPQZUOZGKTZFKTZUVNYKUOZG
      KTZFKTZSZSWIWQUFZUIPZUVKXACRZWTQZSZMXGTZXMUUJUKZGKTFKTZULZUUJUVTUOZGKTFKT
      ZUUJYBCRZUVAQZUOZGKTFKTZSZSWIJWPUFZUFZUIPZUUEUWOULZUXCSUVFUAUBUCUEUDDCJEA
      BHIWLDQZYAUVRYOUWGUXHWSUVIXHUVMXTUVQUXHWRUVHUIUXHWNUVGWQWLDWMUSUTVAUXHXFU
      VLMXGUXHXCUVKXEUXHXBUVJWTXAWLDVBVCVDVEUXHXSUVPFXRKUXHXJXRPZXJKPZXSUVPUXHX
      RKXJUXHXRDUJKWLDVFNVGZVHZUXHXQUVOGXRKUXHXIXRPZXIKPZXQUVOUXHXRKXIUXKVHZUXH
      XPUVNXMUXHXNUUHXOXIWLDVBVCZVIVJVKVJVKVQUXHYHUWCYNUWFUXHYGUWBFXRKUXHUXIUXJ
      YGUWBUXLUXHYFUWAGXRKUXHUXMUXNYFUWAUXOUXHXPUVNYEUVTUXPUXHYCUVSYDUUPYBWLDVB
      XJWLDVBVLVJVJVKVJVKUXHYMUWEFXRKUXHUXIUXJYMUWEUXLUXHYLUWDGXRKUXHUXMUXNYLUW
      DUXOUXHXPUVNYKUXPVMVJVKVJVKVRVRWMCQZUVRUWPUWGUXCUXQUVIUWIUVMUWMUVQUWOUXQU
      VHUWHUIUXQUVGWIWQWMCDVNUTVAUXQUVLUWLMXGUXQXEUWKUVKUXQXDUWJWTXAWMCVBVCVOVE
      UXQUVOUWNFGKKUXQUVNUUJXMUXQXOUUIUUHXJWMCVBVPZVIVSVQUXQUWCUWRUWFUXBUXQUWAU
      WQFGKKUXQUVNUUJUVTUXRVMVSUXQUWDUXAFGKKUXQUVNUUJYKUWTUXRUXQYIUWSYJUVAYBWMC
      VBXIWMCVBVLVJVSVRVRWOJQZUWPUXGUXCUXSUWIUXFUWMUUEUWOUXSUWHUXEUIUXSWQUXDWIW
      OJWPUSVTVAUXSUWLUUDMXGLUXSWTXGPWTLPUWLUUDUXSXGLWTUXSXGJUJLWOJVFOVGVHUXSUV
      KUUAUWKUUCUXSUVJYTWTUXSXAYSDWTWOJVBZWAVCUXSUWJUUBWTUXSXAYSCUXTWAVCVRVJVKW
      BVDWPEQZUXGUUMUXCUVEUYAUXFYRUWOUULUUEUYAUXEWKUIUYAUXDWJWIWPEJVNVTVAUYAUWN
      UUKFGKKUYAXMUUGUUJUYAXLUUFXKWPEVFVHWCVSWGUYAUWRUUSUXBUVDUYAUWQUURFGKKUYAU
      VTUUQUUJUYAUVSUUOUUPUYAYBUUNDXIXJWPEWDZWAVCWEVSUYAUXAUVCFGKKUYAUWTUVBUUJU
      YAUWSUUTUVAUYAYBUUNCUYBWAVCWEVSVRVRWFWH $.
      $( [24-Oct-2007] $)
  $}

  ${
    $d C a c d f g j r $.  $d D a c d f g j r $.  $d J a c d j r $.
    $d M c d j r $.  $d O c d j r $.  $d R c d f g j r $.  $d T c d j r x $.
    $d a c d f g j r x $.
    dedi.1 $e |- D = ( dom_ ` T ) $.
    dedi.2 $e |- C = ( cod_ ` T ) $.
    dedi.3 $e |- J = ( id_ ` T ) $.
    dedi.4 $e |- R = ( o_ ` T ) $.
    dedi.5 $e |- M = dom D $.
    dedi.6 $e |- O = dom J $.
    $( Properties of a deductive system.  (Contributed by FL, 24-Oct-2007.) $)
    dedi $p |- ( T e. Ded
        -> ( ( <. <. D , C >. , <. J , R >. >. e. Alg /\
      A. a e. O
         ( ( D ` ( J ` a ) ) = a /\ ( C ` ( J ` a ) ) = a ) /\
      A. f e. M A. g e. M ( <. g , f >. e. dom R
        <-> ( D ` g ) = ( C ` f ) ) ) /\ (
      A. f e. M A. g e. M
         ( ( D ` g ) = ( C ` f ) -> ( D ` ( g R f ) ) = ( D ` f ) ) /\
      A. f e. M A. g e. M
         ( ( D ` g ) = ( C ` f ) -> ( C ` ( g R f ) ) = ( C ` g ) ) ) ) ) $=
      ( wcel wceq cfv wral vx vd vc vj vr cded cv cop calg wa cdm wb w3a co wex
      wi cab df-ded eleq2i c1st domval eqcomi eqeq2i opeq1 opeq1d eleq1d eqeq1d
      fveq1 anbi1d ralbidv dmeq syl6eqr eleq2d bibi2d imbi12d 3anbi123d eqeq12d
      ralbidv2 imbi1d anbi12d sylbi codval anbi2d eqeq2d 2ralbidv opeq2d fveq2d
      c2nd opeq2 idval 3anbi12d cmpval bibi1d 3anbi13d opreq imbi2d eloi ) DUFQ
      DUAUGUBUGZUCUGZUHZUDUGZUEUGZUHZUHZRXDUIQZJUGZXASZWRSZXFRZXGWSSZXFRZUJZJXA
      UKZTZFUGZEUGZUHZXBUKZQZXOWRSZXPWSSZRZULZFWRUKZTZEYDTZUMZYBXOXPXBUNZWRSZXP
      WRSZRZUPZFYDTZEYDTZYBYHWSSZXOWSSZRZUPZFYDTZEYDTZUJZUJZUJUEUOUDUOUCUOUBUOU
      AUQZQBAUHZGCUHZUHZUIQZXFGSZBSZXFRZUUHASZXFRZUJZJITZXQCUKZQZXOBSZXPASZRZUL
      ZFHTEHTZUMZUUSXOXPCUNZBSZXPBSZRZUPZFHTEHTZUUSUVCASZXOASZRZUPZFHTEHTZUJZUJ
      ZUFUUCDUAEFUDUEJUCUBURUSUUBBWSUHZXCUHZUIQZXGBSZXFRZXKUJZJXMTZXSUUQYARZULZ
      FHTZEHTZUMZUWCYHBSZUVERZUPZFHTZEHTZUWCYQUPZFHTZEHTZUJZUJZUUDXCUHZUIQZUVTX
      GASZXFRZUJZJXMTZXSUUSULZFHTEHTZUMZUUSUWIUPZFHTEHTZUUSYHASZUVJRZUPZFHTEHTZ
      UJZUJZUUDGXBUHZUHZUIQZUUNUXEUMZUXMUJZUVOUAUBUCUEUDDWRDUTSZUTSZRWRBRZUUBUW
      QULUYABWRBUYABDKVAVBVCUYBYGUWGUUAUWPUYBXEUVRXNUWBYFUWFUYBXDUVQUIUYBWTUVPX
      CWRBWSVDVEVFUYBXLUWAJXMUYBXIUVTXKUYBXHUVSXFXGWRBVHVGVIVJUYBYEUWEEYDHUYBXP
      YDQZXPHQZYEUWEUYBYDHXPUYBYDBUKHWRBVKOVLZVMZUYBYCUWDFYDHUYBXOYDQZXOHQZYCUW
      DUYBYDHXOUYEVMZUYBYBUWCXSUYBXTUUQYAXOWRBVHVGZVNVOVRVOVRVPUYBYNUWLYTUWOUYB
      YMUWKEYDHUYBUYCUYDYMUWKUYFUYBYLUWJFYDHUYBUYGUYHYLUWJUYIUYBYBUWCYKUWIUYJUY
      BYIUWHYJUVEYHWRBVHXPWRBVHVQVOVOVRVOVRUYBYSUWNEYDHUYBUYCUYDYSUWNUYFUYBYRUW
      MFYDHUYBUYGUYHYRUWMUYIUYBYBUWCYQUYJVSVOVRVOVRVTVTWAWSUXTWHSZRWSARZUWQUXNU
      LUYKAWSAUYKADLWBVBVCUYLUWGUXFUWPUXMUYLUVRUWSUWBUXCUWFUXEUYLUVQUWRUIUYLUVP
      UUDXCWSABWIVEVFUYLUWAUXBJXMUYLXKUXAUVTUYLXJUWTXFXGWSAVHVGWCVJUYLUWDUXDEFH
      HUYLUWCUUSXSUYLYAUURUUQXPWSAVHWDZVNWEVPUYLUWLUXHUWOUXLUYLUWJUXGEFHHUYLUWC
      UUSUWIUYMVSWEUYLUWMUXKEFHHUYLUWCUUSYQUXJUYMUYLYOUXIYPUVJYHWSAVHXOWSAVHVQV
      OWEVTVTWAXADWHSZUTSZRXAGRZUXNUXSULUYOGXAGUYODGMWJVBVCUYPUXFUXRUXMUYPUWSUX
      QUXCUUNUXEUYPUWRUXPUIUYPXCUXOUUDXAGXBVDWFVFUYPUXBUUMJXMIUYPXFXMQXFIQUXBUU
      MUYPXMIXFUYPXMGUKIXAGVKPVLVMUYPUVTUUJUXAUULUYPUVSUUIXFUYPXGUUHBXFXAGVHZWG
      VGUYPUWTUUKXFUYPXGUUHAUYQWGVGVTVOVRWKVIWAXBUYNWHSZRXBCRZUXSUVOULUYRCXBCUY
      RDCNWLVBVCUYSUXRUVBUXMUVNUYSUXQUUGUXEUVAUUNUYSUXPUUFUIUYSUXOUUEUUDXBCGWIW
      FVFUYSUXDUUTEFHHUYSXSUUPUUSUYSXRUUOXQXBCVKVMWMWEWNUYSUXHUVHUXLUVMUYSUXGUV
      GEFHHUYSUWIUVFUUSUYSUWHUVDUVEUYSYHUVCBXOXPXBCWOZWGVGWPWEUYSUXKUVLEFHHUYSU
      XJUVKUUSUYSUXIUVIUVJUYSYHUVCAUYTWGVGWPWEVTVTWAWQWA $.
      $( [24-Oct-2007] $)
  $}

  ${
    $d A x y z $.
    1ded.1 $e |- A e. _V $.
    $( Category ` 1 ` is a deductive system.  We can think of the morphism of
       Category ` 1 ` as corresponding to ` ph |- ph ` .  (Contributed by FL,
       30-Oct-2007.) $)
    1ded $p |- <. <. { <. <. A , A >. , A >. } ,
      { <. <. A , A >. , A >. } >. , <. { <. A , <. A , A >. >. } ,
      { <. <. <. A , A >. , <. A , A >. >. , <. A , A >. >. } >. >. e. Ded $=
      ( vz vy vx cop csn wcel cv cfv wceq wa cdm co wi cvv fveq2i fveq2d eqcomd
      wral cded calg w3a snex 3pm3.2i dmsnop eqcomi isded mp2an 1alg elsni opex
      wb fvsn eqtri fveq2 id 3eqtr4a syl anidmdbi mpbir rgen snid eleqtrri eqid
      2th opeq12 ancoms eleq1d adantl adantr syl2an rgen2a df-opr opreq2 opreq1
      a1i eqeq12d 3bitr3d sylan9eq syl5ib sylan9eqr pm3.2i mpbir2an ) AAFZAFZGZ
      WGFAWEFZGZWEWEFZWEFZGZFFZUAHZWMUBHZCIZWIJZWGJZWPKZWSLZCAGZTZDIZEIZFZWLMZH
      ZXCWGJZXDWGJZKZUMZDWEGZTEXLTZUCZXJXCXDWLNZWGJZXIKZOZDXLTEXLTZXJXPXHKZOZDX
      LTEXLTZLZWGPHZYDWIPHZUCWLPHWNXNYCLUMYDYDYEWFUDZYFWHUDUEWKUDPPWGWGWLEDPPWI
      XLXACWGMXLWEAUFUGWIMXAAWEUFUGUHUIWOXBXMABUJWTCXAWPXAHZWTOYGWSOYGWPAKZWSWP
      AUKYHAWIJZWGJZAWRWPYJWEWGJZAYIWEWGAWEBAAULZUNQWEAYLBUNUOYHWQYIWGWPAWIUPRY
      HUQURUSYGWSUTVAVBXKEDXLXDXLHZXDWEKZXCWEKZXKXCXLHZXDWEUKZXCWEUKZYNYOLZWJXF
      HZYKYKKZXGXJYTUUAUMYSYTUUAWJWJGXFWJWEWEULZVCWJWEUFVDYKVEVFVQYSWJXEXFYOYNW
      JXEKYOYNLXEWJXCXDWEWEVGSVHVIYSYKXHYKXIYSWEXCWGYOWEXCKYNYOXCWEYOUQSVJRYNYK
      XIKZYOYNWEXDWGYNXDWEYNUQSRVKVRVSVLVMUEXSYBXREDXLYMYNYOXRYPYQYRXJWEWEWLNZW
      GJZYKKZYSXQUUFXJUUDWEWGUUDWJWLJZWEWEWEWLVNZWJWEUUBYLUNZUOQVQYSUUEXPYKXIYN
      YOUUEWEXDWLNZWGJXPYNUUDUUJWGYNUUJUUDXDWEWEWLVOSRYOUUJXOWGYOXOUUJXCWEXDWLV
      PSRVTYNUUCYOYNXIYKXDWEWGUPSVKVRWAVLVMYAEDXLYMYNYOYAYPYQYRXJUUFYSXTUUFXJUU
      EUUGWGJYKUUDUUGWGUUHQUUGWEWGUUIQUOVQYSUUEXPYKXHYSUUDXOWGYOYNUUDXCWEWLNZXO
      YOUUKUUDXCWEWEWLVPSYNXOUUKXDWEXCWLVOSWBRYOYKXHKYNYOXHYKXCWEWGUPSVJVRWAVLV
      MWCWD $.
      $( [30-Oct-2007] $)
  $}


  ${
    $d f t u v w x y z $.
    $( Structure of a deductive system.  (Contributed by FL, 28-Oct-2007.) $)
    strded $p |- Ded C_ ( ( _V X. _V ) X. ( _V X. _V ) ) $=
      ( vx vf vt vw vv vu vz vy cv cop wceq wcel cfv wa cdm wral wi wex cvv cxp
      cded calg wb w3a co cab df-ded stcat eqsstri ) UAAIBIZCIZJDIZEIZJJZKUNUBL
      FIZULMZUJMUOKUPUKMUOKNFULOPGIZHIZJUMOLUQUJMURUKMKZUCGUJOZPHUTPUDUSUQURUMU
      EZUJMURUJMKQGUTPHUTPUSVAUKMUQUKMKQGUTPHUTPNNZNERDRCRBRAUFSSTZVCTAHGDEFCBU
      GVBABCEDUHUI $.
      $( [28-Oct-2007] $)
  $}

  $( A deductive system is a relation.  (Contributed by FL, 28-Oct-2007.) $)
  relded $p |- Rel Ded $=
    ( cded wrel cvv cxp wss strded xpss sstri df-rel mpbir ) ABACCDZEAKKDKFKKGH
    AIJ $.
    $( [28-Oct-2007] $)

  $( The domain of a deductive system is a relation.  (Contributed by FL,
     28-Oct-2007.) $)
  reldded $p |- Rel dom Ded $=
    ( cded cdm wrel cvv cxp wss strded dmss ax-mp dmxpid sseqtri df-rel mpbir )
    ABZCNDDEZFNOOEZBZOAPFNQFGAPHIOJKNLM $.
    $( [28-Oct-2007] $)

  $( The range of a deductive system is a relation.  (Contributed by FL,
     28-Oct-2007.) $)
  relrded $p |- Rel ran Ded $=
    ( cded crn wrel cvv cxp wss strded rnss ax-mp wne wceq vn0 pm3.2i xpnz mpbi
    c0 wa rnxp sseqtri df-rel mpbir ) ABZCUBDDEZFUBUCUCEZBZUCAUDFUBUEFGAUDHIUCP
    JZUEUCKDPJZUGQUFUGUGLLMDDNOUCUCRISUBTUA $.
    $( [28-Oct-2007] $)

  ${
    $d T x y z $.
    $( A deductive system is an "algebra".  (Contributed by FL,
       28-Oct-2007.) $)
    dedalg $p |- ( T e. Ded -> T e. Alg ) $=
      ( vz vy vx cded wcel cfv cop calg c1st c2nd wrel cdm wceq eqid opeq12i cv
      w3a wa wral cdom_ ccod_ cid_ co_ crn relded reldded relrded 11st22nd mpan
      3pm3.2i domval codval idval cmpval syl6eqr wb co wi simpld simp1d eqeltrd
      dedi ) AEFZAAUAGZAUBGZHZAUCGZAUDGZHZHZIVDAAJGZJGZVLKGZHZAKGZJGZVPKGZHZHZV
      KELZEMLZEUELZRVDAVTNWAWBWCUFUGUHUKAEUIUJVGVOVJVSVEVMVFVNVEAVEOZULVFAVFOZU
      MPVHVQVIVRAVHVHOZUNAVIVIOZUOPPUPVDVKIFZBQZVHGZVEGWINWJVFGWINSBVHMZTZCQZDQ
      ZHVIMFWMVEGWNVFGNZUQCVEMZTDWPTZVDWHWLWQRWOWMWNVIURZVEGWNVEGNUSCWPTDWPTWOW
      RVFGWMVFGNUSCWPTDWPTSVFVEVIADCVHWPWKBWDWEWFWGWPOWKOVCUTVAVB $.
      $( [28-Oct-2007] $)
  $}

  ${
    $d A x $.  $d C x y z $.  $d D x y z $.  $d J x $.  $d O x $.  $d T y z $.
    idosd.1 $e |- O = dom J $.
    idosd.2 $e |- D = ( dom_ ` T ) $.
    idosd.3 $e |- J = ( id_ ` T ) $.
    idosd.4 $e |- C = ( cod_ ` T ) $.
    $( The identity is a morphism which has the same object as its domain and
       its codomain.  (Contributed by FL, 28-Oct-2007.) $)
    idosd $p |- ( ( T e. Ded /\ A e. O ) -> ( ( D ` ( J ` A ) ) = A /\
        ( C ` ( J ` A ) ) = A ) ) $=
      ( vx vz vy wcel cfv wceq wa cop cv wral cded co_ calg cdm wb co eqid dedi
      w3a wi fveq2 fveq2d id eqeq12d anbi12d rcla4cv 3ad2ant2 adantr syl imp )
      DUANZAFNZAEOZCOZAPZVCBOZAPZQZVACBREDUBOZRRUCNZKSZEOZCOZVKPZVLBOZVKPZQZKFT
      ZLSZMSZRVIUDNVSCOVTBOPZUELCUDZTMWBTZUIZWAVSVTVIUFZCOVTCOPUJLWBTMWBTWAWEBO
      VSBOPUJLWBTMWBTQZQVBVHUJZBCVIDMLEWBFKHJIVIUGWBUGGUHWDWGWFVRVJWGWCVQVHKAFV
      KAPZVNVEVPVGWHVMVDVKAWHVLVCCVKAEUKZULWHUMZUNWHVOVFVKAWHVLVCBWIULWJUNUOUPU
      QURUSUT $.
      $( [28-Oct-2007] $)
  $}

  ${
    $d C f g x $.  $d D f g x $.  $d F f g $.  $d G g $.  $d M f g $.
    $d R f g $.  $d T x $.
    ded.1 $e |- M = dom D $.
    ded.2 $e |- D = ( dom_ ` T ) $.
    ded.3 $e |- C = ( cod_ ` T ) $.
    ded.4 $e |- R = ( o_ ` T ) $.
    $( ` ( G ( o_ `` T ) F ) ` is only defined when the domain of ` G ` is the
       codomain of ` F ` .  (Contributed by FL, 29-Oct-2007.) $)
    cmppfd $p |- ( ( T e. Ded /\ F e. M /\ G e. M ) ->
          ( <. G , F >. e. dom R <-> ( D ` G ) = ( C ` F ) ) ) $=
      ( vx vg vf wcel cop cfv wceq wa wral cded cdm wb cid_ calg cv w3a co eqid
      wi opeq2 eleq1d fveq2 eqeq2d bibi12d opeq1 eqeq1d rcla42v 3ad2ant3 adantr
      dedi com12 syl 3impib ) DUAOZEGOZFGOZFEPZCUBZOZFBQZEAQZRZUCZVEBAPDUDQZCPP
      UEOZLUFZVOQZBQVQRVRAQVQRSLVOUBZTZMUFZNUFZPZVIOZWABQZWBAQZRZUCZMGTNGTZUGZW
      GWAWBCUHZBQWBBQRUJMGTNGTWGWKAQWAAQRUJMGTNGTSZSVFVGSZVNUJZABCDNMVOGVSLIJVO
      UIKHVSUIVAWJWNWLWIVPWNVTWMWIVNWHVNWAEPZVIOZWEVLRZUCNMEFGGWBERZWDWPWGWQWRW
      CWOVIWBEWAUKULWRWFVLWEWBEAUMUNUOWAFRZWPVJWQVMWSWOVHVIWAFEUPULWSWEVKVLWAFB
      UMUQUOURVBUSUTVCVD $.
      $( [29-Oct-2007] $)


    $( When ` ( G ( o_ `` T ) F ) ` is defined its domain is the domain of
       ` F ` .  (Contributed by FL, 29-Oct-2007.) $)
    domcmpd $p |- ( ( T e. Ded /\ F e. M /\ G e. M ) ->
            ( ( D ` G ) = ( C ` F ) -> ( D ` ( G R F ) ) = ( D ` F ) ) ) $=
      ( vg vf wcel cfv wceq wi cop wa wral vx cded co cid_ calg cv cdm w3a eqid
      wb fveq2 eqeq2d opreq2 fveq2d eqeq12d imbi12d eqeq1d opreq1 rcla42v com12
      dedi ad2antrl syl 3impib ) DUBNZEGNZFGNZFBOZEAOZPZFECUCZBOZEBOZPZQZVEBARD
      UDOZCRRUENUAUFZVPOZBOVQPVRAOVQPSUAVPUGZTLUFZMUFZRCUGNVTBOZWAAOZPZUJLGTMGT
      UHZWDVTWACUCZBOZWABOZPZQZLGTMGTZWDWFAOVTAOPQLGTMGTZSSVFVGSZVOQZABCDMLVPGV
      SUAIJVPUIKHVSUIVAWKWNWEWLWMWKVOWJVOWBVIPZVTECUCZBOZVMPZQMLEFGGWAEPZWDWOWI
      WRWSWCVIWBWAEAUKULWSWGWQWHVMWSWFWPBWAEVTCUMUNWAEBUKUOUPVTFPZWOVJWRVNWTWBV
      HVIVTFBUKUQWTWQVLVMWTWPVKBVTFECURUNUQUPUSUTVBVCVD $.
      $( [29-Oct-2007] $)


    $( When ` ( G ( o_ `` T ) F ) ` is defined its codomain is the codomain of
       ` G ` .  (Contributed by FL, 29-Oct-2007.) $)
    codcmpd $p |- ( ( T e. Ded /\ F e. M /\ G e. M ) ->
             ( ( D ` G ) = ( C ` F )
             -> ( C ` ( G R F ) ) = ( C ` G ) ) ) $=
      ( vg vf wcel cfv wceq wi cop wa wral vx cded co cid_ calg cv cdm w3a eqid
      wb fveq2 eqeq2d opreq2 fveq2d eqeq1d imbi12d opreq1 eqeq12d rcla42v com12
      dedi ad2antll syl 3impib ) DUBNZEGNZFGNZFBOZEAOZPZFECUCZAOZFAOZPZQZVEBARD
      UDOZCRRUENUAUFZVPOZBOVQPVRAOVQPSUAVPUGZTLUFZMUFZRCUGNVTBOZWAAOZPZUJLGTMGT
      UHZWDVTWACUCZBOWABOPQLGTMGTZWDWFAOZVTAOZPZQZLGTMGTZSSVFVGSZVOQZABCDMLVPGV
      SUAIJVPUIKHVSUIVAWLWNWEWGWMWLVOWKVOWBVIPZVTECUCZAOZWIPZQMLEFGGWAEPZWDWOWJ
      WRWSWCVIWBWAEAUKULWSWHWQWIWSWFWPAWAEVTCUMUNUOUPVTFPZWOVJWRVNWTWBVHVIVTFBU
      KUOWTWQVLWIVMWTWPVKAVTFECUQUNVTFAUKURUPUSUTVBVCVD $.
      $( [29-Oct-2007] $)
  $}

  ${
    $d D a $.  $d O a $.  $d T a $.
    rdmob.1 $e |- O = dom ( id_ ` T ) $.
    rdmob.2 $e |- D = ( dom_ ` T ) $.
    $( The range of ` ( dom_ `` T ) ` is the class of the objects.
       (Contributed by FL, 10-Jan-2008.) $)
    rdmob $p |- ( T e. Ded -> ran D = O ) $=
      ( va cded wcel crn calg cdm wf cfv eqid doma 3syl wa wceq wi ex dedalg cv
      wss cid_ frn ccod_ cdom_ idosd ida ffvelrn wfun ffun fvelrn fveq1i eleq1i
      syl syl6ibr syld imp eleq1 rneqi eleq2i syl6bbr biimpd adantr ssrdv eqssd
      sylc ) BGHZAIZCVIBJHZAKZCALVJCUCBUAZABBUDMZVLCVLNEDVNNZOVLCAUEPVIFCVJVIFU
      BZCHZVPVJHZVIVQQVPVNMZAMZVPRZVSBUFMZMVPRZQVTBUGMZIZHZVRVPWBABVNCDEVOWBNUH
      VIVQWFVIVQVSWDKZHZWFVIVKCWGVNLZVQWHSVMWDBVNWGCWGNZWDNZDVOUIWIVQWHCWGVPVNU
      JTPVIVKWGVNKZWDLZWHWFSVMWDBVNWGWLWJWKWLNVOOWMWHVSWDMZWEHZWFWMWDUKZWHWOSWG
      WLWDULWPWHWOVSWDUMTUPVTWNWEVSAWDEUNUOUQPURUSWAWFVRSWCWAWFVRWAWFVPWEHVRVTV
      PWEUTVJWEVPAWDEVAVBVCVDVEVHTVFVG $.
      $( [10-Jan-2008] $)

  $}

  ${
    $d C a $.  $d O a $.  $d T a $.
    rcmob.1 $e |- O = dom ( id_ ` T ) $.
    rcmob.2 $e |- C = ( cod_ ` T ) $.
    $( The range of ` ( cod_ `` T ) ` is the class of the objects.
       (Contributed by FL, 10-Jan-2008.) $)
    rcmob $p |- ( T e. Ded -> ran C = O ) $=
      ( va cded wcel crn calg cdom_ cfv cdm wf wss eqid 3syl wa wceq adantr frn
      dedalg cid_ coda idosd simprd wfun ffun idmoa dcsda eqcomd eleqtrrd sylan
      cv fvelrn syl11anc eqeltrrd ex ssrdv eqssd ) BGHZAIZCVABJHZBKLZMZCANZVBCO
      BUBZAVDBBUCLZVECVEPZVDPZDVHPZEUDZVECAUAQVAFCVBVAFUNZCHZVMVBHVAVNRZVMVHLZA
      LZVMVBVOVPVDLVMSVQVMSVMAVDBVHCDVJVKEUEUFVOAUGZVPAMZHZVQVBHVAVRVNVAVCVFVRV
      GVLVECAUHQTVAVCVNVTVGVCVNRVPVEVSVMVDBVHVECVIVJDVKUIVCVSVESVNVCVEVSAVDBVJE
      UJUKTULUMVPAUOUPUQURUSUT $.
      $( [10-Jan-2008] $)
  $}

  ${
    aidm2.1 $e |- D = ( dom_ ` T ) $.
    aidm2.2 $e |- C = ( cod_ ` T ) $.
    $( The underlying directed multi graph of a deductive system.  (Contributed
       by FL, 20-Sep-2009.) $)
    aidm2 $p |- ( T e. Ded -> <. <. D , C >. , ran D >. e. Dgra ) $=
      ( wcel cop crn cdm wf cfv eqid syl wceq wb feq3 mpbird cdom_ cvv a1i cded
      cmgra wa cid_ calg dedalg doma rdmob dmeqi coda fvex syl6eqel ccod_ rneqd
      jca rnexg eqeltrd ismgra syl111anc ) CUAFZBAGBHZGUBFZBIZVABJZVCVAAJZUCZUT
      VDVEUTVDVCCUDKZIZBJZUTCUEFZVICUFZBCVGVCVHVCLDVHLZVGLZUGMUTVAVHNZVDVIOBCVH
      VLDUHZVAVHVCBPMQUTVEVCVHAJZUTVJVPVKACRKZCVGVCVHBVQDUIVQLVLVMEUJMUTVNVEVPO
      VOVAVHVCAPMQUOUTBSFASFVASFVBVFOUTBVQSBVQNUTDTZCRUKZULUTACUMKZSAVTNUTETCUM
      UKULUTVAVQHZSUTBVQVRUNUTVQSFZWASFWBUTVSTVQSUPMUQSSABVASURUSQ $.
      $( [20-Sep-2009] $)
  $}

  ${
    $d M x $.  $d R a x $.  $d T a x $.
    dmrngcmp.1 $e |- R = ( o_ ` T ) $.
    dmrngcmp.2 $e |- M = dom ( dom_ ` T ) $.
    $( Domain and range of the domain of the composition.  (Contributed by FL,
       5-Oct-2009.) $)
    dmrngcmp $p |- ( T e. Ded ->
    ( dom dom R = M /\ ran dom R = M ) ) $=
      ( vx va wcel cdm wceq crn cv cfv wss eqid wa syl cop sylan syldan cded wi
      calg dedalg co_ wfun cdom_ cxp w3a cmppfa dmss dmxpss sstr eqcomi 3sstr3g
      dmeqi sylancl 3ad2ant2 sseld wex cid_ ccod_ wf doma ffvelrn simprd eqcomd
      idosd simpl idmoa eleq2i biimpi adantl cmppfd syl111anc mpbird fvex opeq2
      wb eleq1d cla4ev visset eldm2 sylibr impbid eqrdv rnss rnxpss coda simpld
      ex rneqi opeq1 elrn2 jca ) BUAHZAIZIZCJWQKZCJWPFWRCWPFLZWRHZWTCHZWPBUCHZX
      AXBUBZBUDZXCBUEMZUFZXFIZBUGMZIZXJUHZNZXFKXJNZUIZXDXIXFBXJXJOZXIOZXFOUJZXN
      WRCWTXLXGWRCNZXMXLXHIZXKIZNZXTXJNZXRXHXKUKXJXJULYAYBPXSXJWRCXSXTXJUMXHWQX
      FAAXFDUNUPZUPCXJEUNZUOUQURUSQQWPXBXAWPXBPZWTGLZRZWQHZGUTZXAYEWTWTXIMZBVAM
      ZMZRZWQHZYIYEYNYJYLBVBMZMZJZYEYPYJYEYLXIMYJJZYPYJJZWPXBYJYKIZHZYRYSPWPCYT
      XIVCZXBUUAWPXCUUBXEXIBYKCYTEXPYTOZYKOZVDZQCYTWTXIVEZSYJYOXIBYKYTUUCXPUUDY
      OOZVHTVFVGYEWPYLXJHZWTXJHZYNYQVSWPXBVIZWPXCXBUUHXEXCXBUUAUUHXCUUBXBUUAUUE
      UUFSYJXIBYKXJYTXOXPUUCUUDVJTSXBUUIWPXBUUICXJWTEVKVLVMZYOXIABYLWTXJXOXPUUG
      DVNVOVPYHYNGYLYJYKVQYFYLJYGYMWQYFYLWTVRVTWAQGWTWQFWBZWCWDWKWEWFWPFWSCWPWT
      WSHZXBWPXCUUMXBUBZXEXCXNUUNXQXNWSCWTXLXGWSCNZXMXLXHKZXKKZNZUUQXJNZUUOXHXK
      WGXJXJWHUURUUSPUUPXJWSCUUPUUQXJUMXHWQYCWLYDUOUQURUSQQWPXBUUMYEYFWTRZWQHZG
      UTZUUMYEWTYOMZYKMZWTRZWQHZUVBYEUVFUVDXIMUVCJZWPXBUVCYTHZUVGWPCYTYOVCZXBUV
      HWPXCUVIXEYOXIBYKCYTEXPUUCUUDUUGWIZQCYTWTYOVEZSWPUVHPUVGUVDYOMUVCJUVCYOXI
      BYKYTUUCXPUUDUUGVHWJTYEWPUUIUVDXJHZUVFUVGVSUUJUUKWPXCXBUVLXEXCXBUVHUVLXCU
      VIXBUVHUVJUVKSUVCXIBYKXJYTXOXPUUCUUDVJTSYOXIABWTUVDXJXOXPUUGDVNVOVPUVAUVF
      GUVDUVCYKVQYFUVDJUUTUVEWQYFUVDWTWMVTWAQGWTWQUULWNWDWKWEWFWO $.
      $( [5-Oct-2009] $)
  $}

