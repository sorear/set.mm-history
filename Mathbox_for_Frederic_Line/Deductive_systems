$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Deductive systems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Ded $. $( The class of all deductive systems. $)

  $( Extend class notation with the class of deductive systems. $)
  cded $a class Ded $.

  ${
    $d x d c j r a f g $.
    $( Definition of a deductive system.  Lambeck and Scott.  Introduction to
       higher order categorical logic. p. 47. 1986.  Unformally we can say a
       deductive system is a directed multi graph where for each object a
       specific morphism called identity of the object exists and where for
       some pairs of morphisms the composite exists.  Deductive system are
       named so because morphisms may be interpreted as logical deductions,
       objects as sets of formulas and compositions as inferences. $)
    df-ded $a |- Ded = { x | E. d E. c E. j E. r
    ( x = <. <. d , c >. , <. j , r >. >. /\ ( (
      <. <. d , c >. , <. j , r >. >. e. Alg /\
      A. a e. dom j
         ( ( d ` ( j ` a ) ) = a /\ ( c ` ( j ` a ) ) = a ) /\
      A. f e. dom d A. g e. dom d ( <. g , f >. e. dom r
        <-> ( d ` g ) = ( c ` f ) ) ) /\ (
      A. f e. dom d A. g e. dom d
         ( ( d ` g ) = ( c ` f ) -> ( d ` ( g r f ) ) = ( d ` f ) ) /\
      A. f e. dom d A. g e. dom d
         ( ( d ` g ) = ( c ` f ) -> ( c ` ( g r f ) ) = ( c ` g ) ) ) ) ) } $.
  $}

  ${
    $d C a c d f g j r $.  $d D a c d f g j r $.  $d J a c d j r $.
    $d M c d j r $.  $d O c d j r $.  $d R c d f g j r $.
    $d a c d f g j r x $.
    isded.1 $e |- M = dom D $.
    isded.2 $e |- O = dom J $.
    $( The predicate "is a deductive system". $)
    isded $p |- ( ( ( D e. A /\ C e. B /\ J e. F ) /\ R e. G )
      -> ( <. <. D , C >. , <. J , R >. >. e. Ded
        <-> ( ( <. <. D , C >. , <. J , R >. >. e. Alg /\
      A. a e. O
         ( ( D ` ( J ` a ) ) = a /\ ( C ` ( J ` a ) ) = a ) /\
      A. f e. M A. g e. M ( <. g , f >. e. dom R
        <-> ( D ` g ) = ( C ` f ) ) ) /\ (
      A. f e. M A. g e. M
         ( ( D ` g ) = ( C ` f ) -> ( D ` ( g R f ) ) = ( D ` f ) ) /\
      A. f e. M A. g e. M
         ( ( D ` g ) = ( C ` f ) -> ( C ` ( g R f ) ) = ( C ` g ) ) ) ) ) ) $=
      ( vx vd vc vj vr wcel w3a wa cop cv wceq calg cfv cdm wral wb co wi wex
      cab cded opeq1 opeq1d eleq1d fveq1 eqeq1d anbi1d ralbidv dmeq syl6eqr
      eleq2d bibi2d imbi12d ralbidv2 3anbi123d eqeq12d imbi1d anbi12d opeq2
      anbi2d eqeq2d 2ralbidv opeq2d fveq2d 3anbi12d bibi1d 3anbi13d opreq
      imbi2d elo df-ded eleq2i syl5bb ) DAUACBUAJHUAUBEIUAUCDCUDZJEUDZUDZPUEQUE
      ZRUEZUDZSUEZTUEZUDZUDZUFWRUGUAZMUEZWOUHZWLUHZWTUFZXAWMUHZWTUFZUCZMWOUIZUJ
      ZGUEZFUEZUDZWPUIZUAZXIWLUHZXJWMUHZUFZUKZGWLUIZUJZFXRUJZUBZXPXIXJWPULZWLUH
      ZXJWLUHZUFZUMZGXRUJZFXRUJZXPYBWMUHZXIWMUHZUFZUMZGXRUJZFXRUJZUCZUCZUCTUNSU
      NRUNQUNPUOZUAWKUGUAZWTJUHZDUHZWTUFZYSCUHZWTUFZUCZMLUJZXKEUIZUAZXIDUHZXJCU
      HZUFZUKZGKUJFKUJZUBZUUJXIXJEULZDUHZXJDUHZUFZUMZGKUJFKUJZUUJUUNCUHZXICUHZU
      FZUMZGKUJFKUJZUCZUCZWKUPUAYPDWMUDZWQUDZUGUAZXADUHZWTUFZXEUCZMXGUJZXMUUHXO
      UFZUKZGKUJZFKUJZUBZUVNYBDUHZUUPUFZUMZGKUJZFKUJZUVNYKUMZGKUJZFKUJZUCZUCWIW
      QUDZUGUAZUVKXACUHZWTUFZUCZMXGUJZXMUUJUKZGKUJFKUJZUBZUUJUVTUMZGKUJFKUJZUUJ
      YBCUHZUVAUFZUMZGKUJFKUJZUCZUCWIJWPUDZUDZUGUAZUUEUWOUBZUXCUCUVFPQRTSDCJEAB
      HIWLDUFZYAUVRYOUWGUXHWSUVIXHUVMXTUVQUXHWRUVHUGUXHWNUVGWQWLDWMUQURUSUXHXFU
      VLMXGUXHXCUVKXEUXHXBUVJWTXAWLDUTVAVBVCUXHXSUVPFXRKUXHXJXRUAZXJKUAZXSUVPUX
      HXRKXJUXHXRDUIKWLDVDNVEZVFZUXHXQUVOGXRKUXHXIXRUAZXIKUAZXQUVOUXHXRKXIUXKVF
      ZUXHXPUVNXMUXHXNUUHXOXIWLDUTVAZVGVHVIVHVIVJUXHYHUWCYNUWFUXHYGUWBFXRKUXHUX
      IUXJYGUWBUXLUXHYFUWAGXRKUXHUXMUXNYFUWAUXOUXHXPUVNYEUVTUXPUXHYCUVSYDUUPYBW
      LDUTXJWLDUTVKVHVHVIVHVIUXHYMUWEFXRKUXHUXIUXJYMUWEUXLUXHYLUWDGXRKUXHUXMUXN
      YLUWDUXOUXHXPUVNYKUXPVLVHVIVHVIVMVMWMCUFZUVRUWPUWGUXCUXQUVIUWIUVMUWMUVQUW
      OUXQUVHUWHUGUXQUVGWIWQWMCDVNURUSUXQUVLUWLMXGUXQXEUWKUVKUXQXDUWJWTXAWMCUTV
      AVOVCUXQUVOUWNFGKKUXQUVNUUJXMUXQXOUUIUUHXJWMCUTVPZVGVQVJUXQUWCUWRUWFUXBUX
      QUWAUWQFGKKUXQUVNUUJUVTUXRVLVQUXQUWDUXAFGKKUXQUVNUUJYKUWTUXRUXQYIUWSYJUVA
      YBWMCUTXIWMCUTVKVHVQVMVMWOJUFZUWPUXGUXCUXSUWIUXFUWMUUEUWOUXSUWHUXEUGUXSWQ
      UXDWIWOJWPUQVRUSUXSUWLUUDMXGLUXSWTXGUAWTLUAUWLUUDUXSXGLWTUXSXGJUILWOJVDOV
      EVFUXSUVKUUAUWKUUCUXSUVJYTWTUXSXAYSDWTWOJUTZVSVAUXSUWJUUBWTUXSXAYSCUXTVSV
      AVMVHVIVTVBWPEUFZUXGUUMUXCUVEUYAUXFYRUWOUULUUEUYAUXEWKUGUYAUXDWJWIWPEJVNV
      RUSUYAUWNUUKFGKKUYAXMUUGUUJUYAXLUUFXKWPEVDVFWAVQWBUYAUWRUUSUXBUVDUYAUWQUU
      RFGKKUYAUVTUUQUUJUYAUVSUUOUUPUYAYBUUNDXIXJWPEWCZVSVAWDVQUYAUXAUVCFGKKUYAU
      WTUVBUUJUYAUWSUUTUVAUYAYBUUNCUYBVSVAWDVQVMVMWEUPYQWKPFGSTMRQWFWGWH $.
      $( [24-Oct-2007] $)
  $}

  ${
    $d C a c d f g j r $.  $d D a c d f g j r $.  $d J a c d j r $.
    $d M c d j r $.  $d O c d j r $.  $d R c d f g j r $.  $d T c d j r x $.
    $d a c d f g j r x $.
    dedi.1 $e |- D = ( dom_ ` T ) $.
    dedi.2 $e |- C = ( cod_ ` T ) $.
    dedi.3 $e |- J = ( id_ ` T ) $.
    dedi.4 $e |- R = ( o_ ` T ) $.
    dedi.5 $e |- M = dom D $.
    dedi.6 $e |- O = dom J $.
    $( Properties of a deductive system. $)
    dedi $p |- ( T e. Ded
        -> ( ( <. <. D , C >. , <. J , R >. >. e. Alg /\
      A. a e. O
         ( ( D ` ( J ` a ) ) = a /\ ( C ` ( J ` a ) ) = a ) /\
      A. f e. M A. g e. M ( <. g , f >. e. dom R
        <-> ( D ` g ) = ( C ` f ) ) ) /\ (
      A. f e. M A. g e. M
         ( ( D ` g ) = ( C ` f ) -> ( D ` ( g R f ) ) = ( D ` f ) ) /\
      A. f e. M A. g e. M
         ( ( D ` g ) = ( C ` f ) -> ( C ` ( g R f ) ) = ( C ` g ) ) ) ) ) $=
      ( vx vd vc vj vr cded wcel cv cop wceq calg cfv wa cdm wral wb w3a co wi
      wex cab df-ded eleq2i c1st domval eqcomi eqeq2i opeq1 opeq1d eleq1d
      fveq1 eqeq1d anbi1d ralbidv dmeq syl6eqr eleq2d bibi2d imbi12d ralbidv2
      3anbi123d eqeq12d imbi1d anbi12d sylbi c2nd codval opeq2 anbi2d eqeq2d
      2ralbidv idval opeq2d fveq2d 3anbi12d cmpval bibi1d 3anbi13d opreq
      imbi2d eloi ) DUBUCDQUDRUDZSUDZUEZTUDZUAUDZUEZUEZUFXDUGUCZJUDZXAUHZWRUHZX
      FUFZXGWSUHZXFUFZUIZJXAUJZUKZFUDZEUDZUEZXBUJZUCZXOWRUHZXPWSUHZUFZULZFWRUJZ
      UKZEYDUKZUMZYBXOXPXBUNZWRUHZXPWRUHZUFZUOZFYDUKZEYDUKZYBYHWSUHZXOWSUHZUFZU
      OZFYDUKZEYDUKZUIZUIZUIUAUPTUPSUPRUPQUQZUCBAUEZGCUEZUEZUGUCZXFGUHZBUHZXFUF
      ZUUHAUHZXFUFZUIZJIUKZXQCUJZUCZXOBUHZXPAUHZUFZULZFHUKEHUKZUMZUUSXOXPCUNZBU
      HZXPBUHZUFZUOZFHUKEHUKZUUSUVCAUHZXOAUHZUFZUOZFHUKEHUKZUIZUIZUBUUCDQEFTUAJ
      SRURUSUUBBWSUEZXCUEZUGUCZXGBUHZXFUFZXKUIZJXMUKZXSUUQYAUFZULZFHUKZEHUKZUMZ
      UWCYHBUHZUVEUFZUOZFHUKZEHUKZUWCYQUOZFHUKZEHUKZUIZUIZUUDXCUEZUGUCZUVTXGAUH
      ZXFUFZUIZJXMUKZXSUUSULZFHUKEHUKZUMZUUSUWIUOZFHUKEHUKZUUSYHAUHZUVJUFZUOZFH
      UKEHUKZUIZUIZUUDGXBUEZUEZUGUCZUUNUXEUMZUXMUIZUVOQRSUATDWRDUTUHZUTUHZUFWRB
      UFZUUBUWQULUYABWRBUYABDKVAVBVCUYBYGUWGUUAUWPUYBXEUVRXNUWBYFUWFUYBXDUVQUGU
      YBWTUVPXCWRBWSVDVEVFUYBXLUWAJXMUYBXIUVTXKUYBXHUVSXFXGWRBVGVHVIVJUYBYEUWEE
      YDHUYBXPYDUCZXPHUCZYEUWEUYBYDHXPUYBYDBUJHWRBVKOVLZVMZUYBYCUWDFYDHUYBXOYDU
      CZXOHUCZYCUWDUYBYDHXOUYEVMZUYBYBUWCXSUYBXTUUQYAXOWRBVGVHZVNVOVPVOVPVQUYBY
      NUWLYTUWOUYBYMUWKEYDHUYBUYCUYDYMUWKUYFUYBYLUWJFYDHUYBUYGUYHYLUWJUYIUYBYBU
      WCYKUWIUYJUYBYIUWHYJUVEYHWRBVGXPWRBVGVRVOVOVPVOVPUYBYSUWNEYDHUYBUYCUYDYSU
      WNUYFUYBYRUWMFYDHUYBUYGUYHYRUWMUYIUYBYBUWCYQUYJVSVOVPVOVPVTVTWAWSUXTWBUHZ
      UFWSAUFZUWQUXNULUYKAWSAUYKADLWCVBVCUYLUWGUXFUWPUXMUYLUVRUWSUWBUXCUWFUXEUY
      LUVQUWRUGUYLUVPUUDXCWSABWDVEVFUYLUWAUXBJXMUYLXKUXAUVTUYLXJUWTXFXGWSAVGVHW
      EVJUYLUWDUXDEFHHUYLUWCUUSXSUYLYAUURUUQXPWSAVGWFZVNWGVQUYLUWLUXHUWOUXLUYLU
      WJUXGEFHHUYLUWCUUSUWIUYMVSWGUYLUWMUXKEFHHUYLUWCUUSYQUXJUYMUYLYOUXIYPUVJYH
      WSAVGXOWSAVGVRVOWGVTVTWAXADWBUHZUTUHZUFXAGUFZUXNUXSULUYOGXAGUYODGMWHVBVCU
      YPUXFUXRUXMUYPUWSUXQUXCUUNUXEUYPUWRUXPUGUYPXCUXOUUDXAGXBVDWIVFUYPUXBUUMJX
      MIUYPXFXMUCXFIUCUXBUUMUYPXMIXFUYPXMGUJIXAGVKPVLVMUYPUVTUUJUXAUULUYPUVSUUI
      XFUYPXGUUHBXFXAGVGZWJVHUYPUWTUUKXFUYPXGUUHAUYQWJVHVTVOVPWKVIWAXBUYNWBUHZU
      FXBCUFZUXSUVOULUYRCXBCUYRDCNWLVBVCUYSUXRUVBUXMUVNUYSUXQUUGUXEUVAUUNUYSUXP
      UUFUGUYSUXOUUEUUDXBCGWDWIVFUYSUXDUUTEFHHUYSXSUUPUUSUYSXRUUOXQXBCVKVMWMWGW
      NUYSUXHUVHUXLUVMUYSUXGUVGEFHHUYSUWIUVFUUSUYSUWHUVDUVEUYSYHUVCBXOXPXBCWOZW
      JVHWPWGUYSUXKUVLEFHHUYSUXJUVKUUSUYSUXIUVIUVJUYSYHUVCAUYTWJVHWPWGVTVTWAWQW
      A $.
      $( [24-Oct-2007] $)
  $}

  ${
    $d A x y z $.
    1ded.1 $e |- A e. _V $.
    $( Category ` 1 ` is a deductive system.  We can think of the morphism of
       Category ` 1 ` as corresponding to ` ph |- ph ` . $)
    1ded $p |- <. <. { <. <. A , A >. , A >. } ,
      { <. <. A , A >. , A >. } >. , <. { <. A , <. A , A >. >. } ,
      { <. <. <. A , A >. , <. A , A >. >. , <. A , A >. >. } >. >. e. Ded $=
      ( vz vy vx cop csn cded wcel calg cv cfv wceq wa wral cdm wb w3a co wi
      cvv snex 3pm3.2i dmsnop eqcomi isded mp2an 1alg elsni opex fvsn fveq2i
      eqtri fveq2 fveq2d id 3eqtr4a syl anidmdbi mpbir rgen snid eleqtrri eqid
      2th a1i opeq12 eqcomd ancoms eleq1d adantl adantr eqeq12d 3bitr3d syl2an
      rgen2a opreq2 opreq1 sylan9eq df-opr syl5bi sylan9eqr pm3.2i mpbir2an )
      AAFZAFZGZWGFAWEFZGZWEWEFZWEFZGZFFZHIZWMJIZCKZWILZWGLZWPMZWSNZCAGZOZDKZEKZ
      FZWLPZIZXCWGLZXDWGLZMZQZDWEGZOEXLOZRZXJXCXDWLSZWGLZXIMZTZDXLOEXLOZXJXPXHM
      ZTZDXLOEXLOZNZWGUAIZYDWIUAIZRWLUAIWNXNYCNQYDYDYEWFUBZYFWHUBUCWKUBUAUAWGWG
      WLEDUAUAWIXLXACWGPXLWEAUDUEWIPXAAWEUDUEUFUGWOXBXMABUHWTCXAWPXAIZWTTYGWSTY
      GWPAMZWSWPAUIYHAWILZWGLZAWRWPYJWEWGLZAYIWEWGAWEBAAUJZUKULWEAYLBUKUMYHWQYI
      WGWPAWIUNUOYHUPUQURYGWSUSUTVAXKEDXLXDWEMZXCWEMZXKXDXLIZXCXLIZYMYNNZWJXFIZ
      YKYKMZXGXJYRYSQYQYRYSWJWJGXFWJWEWEUJZVBWJWEUDVCYKVDVEVFYQWJXEXFYNYMWJXEMY
      NYMNXEWJXCXDWEWEVGVHVIVJYQYKXHYKXIYQWEXCWGYNWEXCMYMYNXCWEYNUPVHVKUOYMYKXI
      MZYNYMWEXDWGYMXDWEYMUPVHUOVLVMVNXDWEUIZXCWEUIZVOVPUCXSYBXREDXLYMYNXRYOYPY
      QWEWEWLSZWGLZYKMZXQXJYQUUEXPYKXIYMYNUUEWEXDWLSZWGLXPYMUUDUUGWGYMUUGUUDXDW
      EWEWLVQVHUOYNUUGXOWGYNXOUUGXCWEXDWLVRVHUOVSYMUUAYNYMXIYKXDWEWGUNVHVLVMUUF
      XJUUDWEWGUUDWJWLLZWEWEWEWLVTZWJWEYTYLUKZUMULVFWAUUBUUCVOVPYAEDXLYMYNYAYOY
      PYQUUFXTXJYQUUEXPYKXHYQUUDXOWGYNYMUUDXCWEWLSZXOYNUUKUUDXCWEWEWLVRVHYMXOUU
      KXDWEXCWLVQVHWBUOYNYKXHMYMYNXHYKXCWEWGUNVHVKVMUUFXJUUEUUHWGLYKUUDUUHWGUUI
      ULUUHWEWGUUJULUMVFWAUUBUUCVOVPWCWD $.
      $( [30-Oct-2007] $)
  $}


  ${
    $d f t u v w x y z $.
    $( Structure of a deductive system. $)
    strded $p |- Ded C_ ( ( _V X. _V ) X. ( _V X. _V ) ) $=
      ( vx vf vt vw vv vu vz vy cded cv cop wceq calg wcel cfv wa cdm wral wb
      w3a co wi wex cab cvv cxp df-ded stcat eqsstri ) IAJBJZCJZKDJZEJZKKZLUNMN
      FJZULOZUJOUOLUPUKOUOLPFULQRGJZHJZKUMQNUQUJOURUKOLZSGUJQZRHUTRTUSUQURUMUAZ
      UJOURUJOLUBGUTRHUTRUSVAUKOUQUKOLUBGUTRHUTRPPZPEUCDUCCUCBUCAUDUEUEUFZVCUFA
      HGDEFCBUGVBABCEDUHUI $.
      $( [28-Oct-2007] $)
  $}

  $( A deductive system is a relation. $)
  relded $p |- Rel Ded $=
    ( cded wrel cvv cxp wss strded xpss sstri df-rel mpbir ) ABACCDZEAKKDKFKKGH
    AIJ $.
    $( [28-Oct-2007] $)

  $( The domain of a deductive system is a relation. $)
  reldded $p |- Rel dom Ded $=
    ( cded cdm wrel cvv cxp wss strded dmss ax-mp dmxpid sseqtri df-rel mpbir
    ) ABZCNDDEZFNOOEZBZOAPFNQFGAPHIOJKNLM $.
    $( [28-Oct-2007] $)

  $( The range of a deductive system is a relation. $)
  relrded $p |- Rel ran Ded $=
    ( cded crn wrel cvv cxp wss strded rnss ax-mp c0 wne wceq wa vn0 pm3.2i
    xpnz mpbi rnxp sseqtri df-rel mpbir ) ABZCUBDDEZFUBUCUCEZBZUCAUDFUBUEFGAUDH
    IUCJKZUEUCLDJKZUGMUFUGUGNNODDPQUCUCRISUBTUA $.
    $( [28-Oct-2007] $)

  ${
    $d T x y z $.
    $( A deductive system is an "algebra". $)
    dedalg $p |- ( T e. Ded -> T e. Alg ) $=
      ( vz vy vx cded wcel cdom_ cfv ccod_ cop cid_ co_ calg c1st c2nd wrel
      cdm crn w3a wceq relded reldded relrded 3pm3.2i 11st22nd mpan eqid
      domval codval opeq12i idval cmpval syl6eqr cv wa wral wb co wi dedi
      pm3.26d 3simp1d eqeltrd ) AEFZAAGHZAIHZJZAKHZALHZJZJZMVDAANHZNHZVLOHZJZAO
      HZNHZVPOHZJZJZVKEPZEQPZERPZSVDAVTTWAWBWCUAUBUCUDAEUEUFVGVOVJVSVEVMVFVNVEA
      VEUGZUHVFAVFUGZUIUJVHVQVIVRAVHVHUGZUKAVIVIUGZULUJUJUMVDVKMFZBUNZVHHZVEHWI
      TWJVFHWITUOBVHQZUPZCUNZDUNZJVIQFWMVEHWNVFHTZUQCVEQZUPDWPUPZVDWHWLWQSWOWMW
      NVIURZVEHWNVEHTUSCWPUPDWPUPWOWRVFHWMVFHTUSCWPUPDWPUPUOVFVEVIADCVHWPWKBWDW
      EWFWGWPUGWKUGUTVAVBVC $.
      $( [28-Oct-2007] $)
  $}

  ${
    $d A x $.  $d C x y z $.  $d D x y z $.  $d J x $.  $d O x $.  $d T y z $.
    idosd.1 $e |- O = dom J $.
    idosd.2 $e |- D = ( dom_ ` T ) $.
    idosd.3 $e |- J = ( id_ ` T ) $.
    idosd.4 $e |- C = ( cod_ ` T ) $.
    $( The identity is a morphism which has the same object as its domain and
       its codomain. $)
    idosd $p |- ( ( T e. Ded /\ A e. O ) -> ( ( D ` ( J ` A ) ) = A /\
        ( C ` ( J ` A ) ) = A ) ) $=
      ( vx vz vy cded wcel cfv wceq wa cop co_ calg cv wral cdm wb w3a co wi
      eqid dedi fveq2 fveq2d id eqeq12d anbi12d rcla4cv 3ad2ant2 adantr syl
      imp ) DNOZAFOZAEPZCPZAQZVCBPZAQZRZVACBSEDTPZSSUAOZKUBZEPZCPZVKQZVLBPZVKQZ
      RZKFUCZLUBZMUBZSVIUDOVSCPVTBPQZUELCUDZUCMWBUCZUFZWAVSVTVIUGZCPVTCPQUHLWBU
      CMWBUCWAWEBPVSBPQUHLWBUCMWBUCRZRVBVHUHZBCVIDMLEWBFKHJIVIUIWBUIGUJWDWGWFVR
      VJWGWCVQVHKAFVKAQZVNVEVPVGWHVMVDVKAWHVLVCCVKAEUKZULWHUMZUNWHVOVFVKAWHVLVC
      BWIULWJUNUOUPUQURUSUT $.
      $( [28-Oct-2007] $)
  $}

  ${
    $d C f g x $.  $d D f g x $.  $d F f g $.  $d G g $.  $d M f g $.
    $d R f g $.  $d T x $.
    ded.1 $e |- M = dom D $.
    ded.2 $e |- D = ( dom_ ` T ) $.
    ded.3 $e |- C = ( cod_ ` T ) $.
    ded.4 $e |- R = ( o_ ` T ) $.
    $( ` ( G ( o_ `` T ) F ) ` is only defined when the domain of ` G ` is the
       codomain of ` F ` . $)
    cmppfd $p |- ( ( T e. Ded /\ F e. M /\ G e. M ) ->
          ( <. G , F >. e. dom R <-> ( D ` G ) = ( C ` F ) ) ) $=
      ( vx vg vf cded wcel cop cdm cfv wceq wb cid_ calg cv wa wral w3a co wi
      eqid dedi opeq2 eleq1d fveq2 eqeq2d bibi12d opeq1 eqeq1d rcla42v com12
      3ad2ant3 adantr syl 3impib ) DOPZEGPZFGPZFEQZCRZPZFBSZEASZTZUAZVEBAQDUBSZ
      CQQUCPZLUDZVOSZBSVQTVRASVQTUELVORZUFZMUDZNUDZQZVIPZWABSZWBASZTZUAZMGUFNGU
      FZUGZWGWAWBCUHZBSWBBSTUIMGUFNGUFWGWKASWAASTUIMGUFNGUFUEZUEVFVGUEZVNUIZABC
      DNMVOGVSLIJVOUJKHVSUJUKWJWNWLWIVPWNVTWMWIVNWHVNWAEQZVIPZWEVLTZUANMEFGGWBE
      TZWDWPWGWQWRWCWOVIWBEWAULUMWRWFVLWEWBEAUNUOUPWAFTZWPVJWQVMWSWOVHVIWAFEUQU
      MWSWEVKVLWAFBUNURUPUSUTVAVBVCVD $.
      $( [29-Oct-2007] $)


    $( When ` ( G ( o_ `` T ) F ) ` is defined its domain is the domain of
       ` F ` . $)
    domcmpd $p |- ( ( T e. Ded /\ F e. M /\ G e. M ) ->
            ( ( D ` G ) = ( C ` F ) -> ( D ` ( G R F ) ) = ( D ` F ) ) ) $=
      ( vx vg vf cded wcel cfv wceq co wi cop cid_ calg cv wa cdm wral wb w3a
      eqid dedi fveq2 eqeq2d opreq2 fveq2d eqeq12d imbi12d eqeq1d opreq1
      rcla42v com12 ad2antrl syl 3impib ) DOPZEGPZFGPZFBQZEAQZRZFECSZBQZEBQZRZT
      ZVEBAUADUBQZCUAUAUCPLUDZVPQZBQVQRVRAQVQRUELVPUFZUGMUDZNUDZUACUFPVTBQZWAAQ
      ZRZUHMGUGNGUGUIZWDVTWACSZBQZWABQZRZTZMGUGNGUGZWDWFAQVTAQRTMGUGNGUGZUEUEVF
      VGUEZVOTZABCDNMVPGVSLIJVPUJKHVSUJUKWKWNWEWLWMWKVOWJVOWBVIRZVTECSZBQZVMRZT
      NMEFGGWAERZWDWOWIWRWSWCVIWBWAEAULUMWSWGWQWHVMWSWFWPBWAEVTCUNUOWAEBULUPUQV
      TFRZWOVJWRVNWTWBVHVIVTFBULURWTWQVLVMWTWPVKBVTFECUSUOURUQUTVAVBVCVD $.
      $( [29-Oct-2007] $)


    $( When ` ( G ( o_ `` T ) F ) ` is defined its codomain is the codomain of
       ` G ` . $)
    codcmpd $p |- ( ( T e. Ded /\ F e. M /\ G e. M ) ->
             ( ( D ` G ) = ( C ` F )
             -> ( C ` ( G R F ) ) = ( C ` G ) ) ) $=
      ( vx vg vf cded wcel cfv wceq co wi cop cid_ calg cv wa cdm wral wb w3a
      eqid dedi fveq2 eqeq2d opreq2 fveq2d eqeq1d imbi12d opreq1 eqeq12d
      rcla42v com12 ad2antll syl 3impib ) DOPZEGPZFGPZFBQZEAQZRZFECSZAQZFAQZRZT
      ZVEBAUADUBQZCUAUAUCPLUDZVPQZBQVQRVRAQVQRUELVPUFZUGMUDZNUDZUACUFPVTBQZWAAQ
      ZRZUHMGUGNGUGUIZWDVTWACSZBQWABQRTMGUGNGUGZWDWFAQZVTAQZRZTZMGUGNGUGZUEUEVF
      VGUEZVOTZABCDNMVPGVSLIJVPUJKHVSUJUKWLWNWEWGWMWLVOWKVOWBVIRZVTECSZAQZWIRZT
      NMEFGGWAERZWDWOWJWRWSWCVIWBWAEAULUMWSWHWQWIWSWFWPAWAEVTCUNUOUPUQVTFRZWOVJ
      WRVNWTWBVHVIVTFBULUPWTWQVLWIVMWTWPVKAVTFECURUOVTFAULUSUQUTVAVBVCVD $.
      $( [29-Oct-2007] $)
  $}

  ${
    $d D a $.  $d O a $.  $d T a $.
    rdmob.1 $e |- O = dom ( id_ ` T ) $.
    rdmob.2 $e |- D = ( dom_ ` T ) $.
    $( The range of ` ( dom_ `` T ) ` is the class of the objects. $)
    rdmob $p |- ( T e. Ded -> ran D = O ) $=
      ( va cded wcel crn calg cdm wf wss dedalg cid_ cfv eqid doma frn 3syl cv
      wceq ccod_ wa cdom_ wi eleq1 rneqi eleq2i syl6bbr biimpd adantr idosd
      ida ffvelrn ex wfun ffun fvelrn syl fveq1i eleq1i syl6ibr syld imp sylc
      ssrdv eqssd ) BGHZAIZCVIBJHZAKZCALVJCMBNZABBOPZVLCVLQEDVNQZRVLCASTVIFCVJV
      IFUAZCHZVPVJHZVPVNPZAPZVPUBZVSBUCPZPVPUBZUDVTBUEPZIZHZVRVIVQUDWAWFVRUFWCW
      AWFVRWAWFVPWEHVRVTVPWEUGVJWEVPAWDEUHUIUJUKULVPWBABVNCDEVOWBQUMVIVQWFVIVQV
      SWDKZHZWFVIVKCWGVNLZVQWHUFVMWDBVNWGCWGQZWDQZDVOUNWIVQWHCWGVPVNUOUPTVIVKWG
      VNKZWDLZWHWFUFVMWDBVNWGWLWJWKWLQVORWMWHVSWDPZWEHZWFWMWDUQZWHWOUFWGWLWDURW
      PWHWOVSWDUSUPUTVTWNWEVSAWDEVAVBVCTVDVEVFUPVGVH $.
      $( [10-Jan-2008] $)

  $}

  ${
    $d C a $.  $d O a $.  $d T a $.
    rcmob.1 $e |- O = dom ( id_ ` T ) $.
    rcmob.2 $e |- C = ( cod_ ` T ) $.
    $( The range of ` ( cod_ `` T ) ` is the class of the objects. $)
    rcmob $p |- ( T e. Ded -> ran C = O ) $=
      ( va cded wcel crn calg cdom_ cfv cdm wf wss dedalg cid_ eqid coda frn
      3syl cv wa wceq idosd pm3.27d wfun fvelrn ffun adantr idmoa dcsda eqcomd
      eleqtrrd sylan sylanc eqeltrrd ex ssrdv eqssd ) BGHZAIZCVABJHZBKLZMZCANZV
      BCOBPZAVDBBQLZVECVERZVDRZDVHRZESZVECATUAVAFCVBVAFUBZCHZVMVBHVAVNUCZVMVHLZ
      ALZVMVBVOVPVDLVMUDVQVMUDVMAVDBVHCDVJVKEUEUFAUGZVPAMZHZVQVBHVOVPAUHVAVRVNV
      AVCVFVRVGVLVECAUIUAUJVCVNVTVAVCVNUCVPVEVSVMVDBVHVECVIVJDVKUKVCVSVEUDVNVCV
      EVSAVDBVJEULUMUJUNVGUOUPUQURUSUT $.
      $( [10-Jan-2008] $)
  $}

  ${
    aidm2.1 $e |- D = ( dom_ ` T ) $.
    aidm2.2 $e |- C = ( cod_ ` T ) $.
    $( The underlying directed multi graph of a deductive system. $)
    aidm2 $p |- ( T e. Ded -> <. <. D , C >. , ran D >. e. Dgra ) $=
      ( cded wcel cop crn cmgra cdm wf wa cid_ cfv calg dedalg eqid doma syl
      wceq wb rdmob feq3 mpbird cdom_ dmeqi coda jca cvv ismgra a1i fvex
      syl6eqel ccod_ rneqd rnexg eqeltrd syl3ancOLD ) CFGZBAHBIZHJGZBKZVABLZVCVAAL
      ZMZUTVDVEUTVDVCCNOZKZBLZUTCPGZVICQZBCVGVCVHVCRDVHRZVGRZSTUTVAVHUAZVDVIUBB
      CVHVLDUCZVAVHVCBUDTUEUTVEVCVHALZUTVJVPVKACUFOZCVGVCVHBVQDUGVQRVLVMEUHTUTV
      NVEVPUBVOVAVHVCAUDTUEUIBUJGAUJGVAUJGVBVFUBUTUJUJABVAUJUKUTBVQUJBVQUAUTDUL
      ZCUFUMZUNUTACUOOZUJAVTUAUTEULCUOUMUNUTVAVQIZUJUTBVQVRUPUTVQUJGZWAUJGWBUTV
      SULVQUJUQTURUSUE $.
      $( [20-Sep-2009] $)
  $}

  ${
    $d M x $.  $d R a x $.  $d T a x $.
    dmrngcmp.1 $e |- R = ( o_ ` T ) $.
    dmrngcmp.2 $e |- M = dom ( dom_ ` T ) $.
    $( Domain and range of the domain of the composition. $)
    dmrngcmp $p |- ( T e. Ded ->
    ( dom dom R = M /\ ran dom R = M ) ) $=
      ( vx va cded wcel cdm wceq crn cv calg wi dedalg co_ cfv wfun cdom_ cxp
      wss w3a eqid cmppfa dmss dmxpss wa sstr eqcomi dmeqi 3sstr3g mpan2 syl
      3ad2ant2 sseld cop wex cid_ ccod_ wf ffvelrn doma sylan idosd syldan
      pm3.27d eqcomd wb cmppfd pm3.26 idmoa eleq2i biimpi adantl syl3ancOLD
      mpbird fvex opeq2 eleq1d cla4ev visset eldm2 sylibr ex impbid eqrdv rnss
      rnxpss rneqi coda pm3.26d opeq1 elrn2 jca ) BHIZAJZJZCKWQLZCKWPFWRCWPFMZW
      RIZWTCIZWPBNIZXAXBOZBPZXCBQRZSZXFJZBTRZJZXJUAZUBZXFLXJUBZUCZXDXIXFBXJXJUD
      ZXIUDZXFUDUEZXNWRCWTXLXGWRCUBZXMXLXHJZXKJZUBZXRXHXKUFYAXTXJUBZXRXJXJUGYAY
      BUHXSXJWRCXSXTXJUIXHWQXFAAXFDUJUKZUKCXJEUJZULUMUNUOUPUNUNWPXBXAWPXBUHZWTG
      MZUQZWQIZGURZXAYEWTWTXIRZBUSRZRZUQZWQIZYIYEYNYJYLBUTRZRZKZYEYPYJYEYLXIRYJ
      KZYPYJKZWPXBYJYKJZIZYRYSUHCYTXIVAZXBUUAWPCYTWTXIVBZWPXCUUBXEXIBYKCYTEXPYT
      UDZYKUDZVCZUNVDYJYOXIBYKYTUUDXPUUEYOUDZVEVFVGVHWPYLXJIZWTXJIZYNYQVIYEYOXI
      ABYLWTXJXOXPUUGDVJWPXBVKZXCXBUUHWPXCXBUUAUUHUUBXBUUAXCUUCUUFVDYJXIBYKXJYT
      XOXPUUDUUEVLVFXEVDXBUUIWPXBUUICXJWTEVMVNVOZVPVQYHYNGYLYJYKVRYFYLKYGYMWQYF
      YLWTVSVTWAUNGWTWQFWBZWCWDWEWFWGWPFWSCWPWTWSIZXBWPXCUUMXBOZXEXCXNUUNXQXNWS
      CWTXLXGWSCUBZXMXLXHLZXKLZUBZUUOXHXKWHUURUUQXJUBZUUOXJXJWIUURUUSUHUUPXJWSC
      UUPUUQXJUIXHWQYCWJYDULUMUNUOUPUNUNWPXBUUMYEYFWTUQZWQIZGURZUUMYEWTYORZYKRZ
      WTUQZWQIZUVBYEUVFUVDXIRUVCKZWPXBUVCYTIZUVGCYTYOVAZXBUVHWPCYTWTYOVBZWPXCUV
      IXEYOXIBYKCYTEXPUUDUUEUUGWKZUNVDWPUVHUHUVGUVDYORUVCKUVCYOXIBYKYTUUDXPUUEU
      UGVEWLVFWPUUIUVDXJIZUVFUVGVIYEYOXIABWTUVDXJXOXPUUGDVJUUJUUKXCXBUVLWPXCXBU
      VHUVLUVIXBUVHXCUVJUVKVDUVCXIBYKXJYTXOXPUUDUUEVLVFXEVDVPVQUVAUVFGUVDUVCYKV
      RYFUVDKUUTUVEWQYFUVDWTWMVTWAUNGWTWQUULWNWDWEWFWGWO $.
      $( [5-Oct-2009] $)
  $}

