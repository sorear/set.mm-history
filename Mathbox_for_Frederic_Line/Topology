$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topology
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( A topology is not empty.  (Contributed by FL, 1-Jun-2008.) $)
  topnem $p |- ( J e. Top -> J =/= (/) ) $=
    ( ctop wcel c0 wn wne 0ntop wa wceq nelneq df-ne sylibr mpan2 ) ABCZDBCEZAD
    FZGNOHADIEPADBJADKLM $.
    $( [1-Jun-2008] $)

  ${
    clsint.1 $e |- X = U. J $.
    $( The closure of an intersection is included in the intersection of the
       closures.  (Contributed by FL, 23-Feb-2009.) $)
    clsint $p |- ( ( J e. Top /\ S C_ X /\ T C_ X )
     -> ( ( cls ` J ) ` ( S i^i T ) ) C_
     ( ( ( cls ` J ) ` S ) i^i ( ( cls ` J ) ` S ) ) ) $=
      ( ctop wcel wss w3a cin ccl cfv inss1 a1i clsss syld3an3 jca ssin sylib
      wa ) CFGZADHZBDHZIZABJZCKLZLZAUFLZHZUITUGUHUHJHUDUIUIUAUBUCUEAHZUIUJUDABM
      NAUECDEOPZUKQUGUHUHRS $.
      $( [23-Feb-2009] $)
  $}

  ${
    $d J x $.  $d P x $.  $d S x $.  $d X x $.
    islp3.1 $e |- X = U. J $.
    $( The predicate " ` P ` is a limit point of ` S ` " in terms of open sets.
       see ~ islp2 , ~ elcls , ~ islp .  (Contributed by FL, 31-Jul-2009.) $)
    islp3 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
     ( P e. ( ( limPt ` J ) ` S ) <->
        A. x e. J ( P e. x -> ( x i^i ( S \ { P } ) ) =/= (/) ) ) ) $=
      ( ctop wcel wss w3a clp cfv csn cdif ccl cv cin c0 wne wb wi wral 3adant3
      islp wa simp2 difss jctil sstr syl elcls syld3an2 bitrd ) DGHZCEIZBEHZJZB
      CDKLLHZBCBMZNZDOLLHZBAPZHVBUTQRSUAADUBZUNUOURVATUPBCDEFUDUCUNUTEIZUOUPVAV
      CTUQUTCIZUOUEVDUQUOVEUNUOUPUFCUSUGUHUTCEUIUJABUTDEFUKULUM $.
      $( [31-Jul-2009] $)
  $}

  ${
    $d I u v x $.  $d J u v $.
    $( The intersection of a family of topologies is a topology.  (Contributed
       by FL, 19-Sep-2011.) $)
    inttop2 $p |- ( ( I =/= (/) /\ A. x e. I J e. Top )
      -> |^|_ x e. I J e. Top ) $=
      ( vu vv ctop wcel wral wa cv wss wi ral2imi adantl cvv wb vex eliin ax-mp
      com12 c0 wne ciin cuni wal cin uniopn ex ssiin uniex 3imtr4g r19.26 inopn
      alrimiv 3expib sylbir syl6ibr syl2anb ralrimivv jca iinexg istopg mpbird
      inex1 syl ) BUAUBZCFGZABHZIZABCUCZFGZDJZVJKZVLUDZVJGZLZDUEZVLEJZUFZVJGZEV
      JHDVJHZIZVIVQWAVIVPDVIVLCKZABHZVNCGZABHZVMVOVHWDWFLVFVGWCWEABVGWCWEVLCUGU
      HMNABCVLUIVNOGVOWFPVLDQZUJAVNBCORSUKUNVIVTDEVJVJVLVJGZVRVJGZIVIVTWHVLCGZA
      BHZVRCGZABHZVIVTLWIVLOGWHWKPWGAVLBCORSVROGWIWMPEQAVRBCORSWKWMIZVIVSCGZABH
      ZVTWNWJWLIZABHZVIWPLWJWLABULVIWRWPVHWRWPLVFVGWQWOABVGWJWLWOVLVRCUMUOMNTUP
      VSOGVTWPPVLVRWGVDAVSBCORSUQURTUSUTVIVJOGVKWBPABCFVADEOVJVBVEVC $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d J i $.
    $( The intersection of a family of topologies is a topology.  (Contributed
       by FL, 19-Sep-2011.) $)
    inttop3 $p |- ( ( J =/= (/) /\ J C_ Top )
      -> |^| J e. Top ) $=
      ( vi c0 wne ctop wss wa cint ciin intiin wcel wral dfss3 inttop2 syl5eqel
      cv sylan2b ) ACDZAEFZGAHBABPZIZEBAJSRTEKBALUAEKBAEMBATNQO $.
      $( [19-Sep-2011] $)
  $}

  $( The intersection of two topologies is a topology.  (Contributed by FL,
     19-Sep-2011.) $)
  inttop4 $p |- ( ( J e. Top /\ K e. Top )
      -> ( J i^i K ) e. Top ) $=
    ( ctop wcel wa cin cpr wss prssg biimpd cint intprg eqcomd adantl wi c0 wne
    wceq prnzg ex inttop3 syl adantr impcom eqeltrd syli pm2.43i ) ACDZBCDZEZAB
    FZCDZUJUJABGZCHZULUJUJUNABCCCIJUNUJULUNUJEUKUMKZCUJUKUORUNUJUOUKABCCLMNUJUN
    UOCDZUHUNUPOZUIUHUMPQZUQABCSURUNUPUMUATUBUCUDUETUFUG $.
    $( [19-Sep-2011] $)

  ${
    $d J x y $.  $d K x y $.
    $( The intersection of two topologies over the same underlying set ` U. J `
       is a topology over ` U. J ` . compare ~ uniin .  (Contributed by FL,
       27-Nov-2011.) $)
    unint2t $p |- ( ( J e. Top /\ K e. Top /\ U. J = U. K )
     -> U. ( J i^i K ) = U. J ) $=
      ( vx vy ctop wcel cuni wceq w3a cin wss uniin inss1 a1i sstr cv wi topopn
      eqid syl sylancr wrex wral wb eleq1 eqcoms wa biimpri expcom syl6bi com3l
      elin syl5com 3imp elssuni sseq2 rcla4ev syl2an ralrimiva uniss2 eqssd ) A
      EFZBEFZAGZBGZHZIZABJZGZVDVGVIVDVEJZKVJVDKZVIVDKABLVKVGVDVEMNVIVJVDOUAVGCP
      ZDPZKZDVHUBZCAUCVDVIKVGVOCAVGVDVHFZVLVDKZVOVLAFVBVCVFVPVBVDAFZVCVFVPQZAVD
      VDSRVCVEBFZVRVSQBVEVESRVFVTVRVPVFVTVDBFZVRVPQVTWAUDVEVDVEVDBUEUFVRWAVPVPV
      RWAUGVDABULUHUIUJUKTUMUNVLAUOVNVQDVDVHVMVDVLUPUQURUSCDAVHUTTVA $.
      $( [27-Nov-2011] $)
  $}

  ${
    $d J a b $.  $d J a x $.  $d T a b $.  $d T a x $.
    $( The intersection of a family of topologies over the same underying set
       ` U. J ` is a topology over ` U. J ` .  (Contributed by FL,
       27-Nov-2011.) $)
    intfmu2 $p |- ( ( T C_ Top
      /\ J e. T /\ A. x e. T U. x = U. J )
         -> U. |^| T = U. J ) $=
      ( va vb ctop wss wcel cv cuni wceq wral w3a intss1 3ad2ant2 syl wa wb cvv
      cint uniss wrex wi ssel eqid topopn syl6 adantr imp eleq1 eqcoms ralimdva
      3impia uniexg elintg mpbird elssuni sseq2 rcla4ev syl2an ralrimiva uniss2
      syl5ibrcom eqssd ) BFGZCBHZAIZJZCJZKZABLZMZBTZJZVIVLVMCGZVNVIGVFVEVOVKCBN
      OVMCUAPVLDIZEIZGZEVMUBZDCLVIVNGVLVSDCVLVIVMHZVPVIGZVSVPCHVLVTVIVGHZABLZVE
      VFVKWCVEVFQZVJWBABWDVGBHZQWBVJVHVGHZWDWEWFVEWEWFUCVFVEWEVGFHWFBFVGUDVGVHV
      HUEUFUGUHUIWBWFRVIVHVIVHVGUJUKVCULUMVLVISHZVTWCRVFVEWGVKCBUNOAVIBSUOPUPVP
      CUQVRWAEVIVMVQVIVPURUSUTVADECVMVBPVD $.
      $( [27-Nov-2011] $)
  $}

  ${
    $d A a $.  $d J a $.
    $( The intersection of a non-empty finite family of open sets is open.
       (Contributed by FL, 20-Apr-2012.) $)
    neffopo $p |- ( J e. Top -> ( ( A C_ J /\ A =/= (/) /\ A e. Fin )
        -> |^| A e. J ) ) $=
      ( va wss c0 wne cfn wcel w3a ctop cint wi elpwg cv wceq sseq1 neeq1 com12
      cpw wal eleq1 3anbi123d inteq eleq1d imbi2d imbi12d wa istop2g ibi adantl
      cuni ax4 syl vtoclg 3exp com3r com4r syl6bir pm2.43a com4l pm2.43i 3imp )
      ABDZAEFZAGHZIZBJHZAKZBHZVCVDVEVGVILZVCVDVEVJLLVEVCVCVDVJVCVEVCVDVJLLZVEVC
      ABSZHZVEVKLABGMVEVCVDVMVJVCVDVEVMVJLZVCVDVEVNVMVFVJCNZBDZVOEFZVOGHZIZVGVO
      KZBHZLZLVFVJLCAVLVOAOZVSVFWBVJWCVPVCVQVDVRVEVOABPVOAEQVOAGUAUBWCWAVIVGWCV
      TVHBVOAUCUDUEUFVGVSWAVGVPVOUKBHLCTZVSWALZCTZUGZWEVGWGCJBUHUIWFWEWDWECULUJ
      UMRUNRUOUPUQURUSUTVAVBR $.
      $( [20-Apr-2012] $)
  $}

  ${
    $( If a subbase is included into a topology, so is the generated topology.
       (Contributed by FL, 20-Apr-2012.) $)
    sbincgt $p |- ( ( J e. Top /\ A C_ J ) -> ( topGen ` ( fi ` A ) ) C_ J ) $=
      ( ctop wcel wss cfi cfv ctg wceq wi tgtop ctb w3a ex sseq2 imbi2d syl5ibr
      eqcoms mpcom imp wa fibas a1i topbas adantr fitop fiss 3jca tgss syl6 wb
      ) BCDZABEZAFGZHGZBEZBHGZBIZULUMUPJZBKULUSURUMUOUQEZJULUMUNLDZBLDZUNBEZMZU
      TULUMVDULUMUAZVAVBVCVAVEAUBUCULVBUMBUDUEULUMVCBFGZBIULUMVCJZBUFULVGJBVFUL
      VGBVFIZUMUNVFEZJULUMVIABCUGNVHVCVIUMBVFUNOPQRSTUHNUNBUIUJURUPUTUMUPUTUKBU
      QBUQUOORPQST $.
      $( [25-Nov-2013] $) $( [20-Apr-2012] $)
  $}

  ${
    $d A v $.  $d J v $.  $d X v $.
    apnei.1 $e |- X = U. J $.
    $( Any point has a neighborhood.  (Contributed by FL, 15-Oct-2012.) $)
    apnei $p |- ( ( J e. Top /\ A e. X ) -> E. v v e. ( ( nei ` J ) ` { A } ) )
        $=
      ( ctop wcel wa cv csn cnei cfv wex wss snssi tpnei syl5ibcom impcom eleq1
      wrex rcla4ev syl2anc df-rex anidm exbii bitri sylib ) CFGZBDGZHZAIZBJZCKL
      LZGZAUMTZUNAMZUJDUMGZUQUOUIUHUQUIULDNUHUQBDOULCDEPQRZURUNUQADUMUKDUMSUAUB
      UOUNUNHZAMUPUNAUMUCUSUNAUNUDUEUFUG $.
      $( [15-Oct-2012] $)
  $}

  ${
    $( A neighborhood of a point can't be empty.  (Contributed by FL,
       15-Oct-2012.) $)
    npmp $p |- ( ( J e. Top /\ A e. X /\ N e. ( ( nei ` J ) ` { A } ) )
     -> N =/= (/) ) $=
      ( ctop wcel csn cnei cfv w3a c0 wne wa wn simp1 snnzg 3ad2ant2 jca 0nnei
      wi nelne2 ex 3ad2ant3 com12 3syl pm2.43i ) BEFZADFZCAGZBHIIZFZJZCKLZULUGU
      IKLZMKUJFNZULUMTULUGUNUGUHUKOUHUGUNUKADPQRUIBSULUOUMUKUGUOUMTUHUKUOUMCKUJ
      UAUBUCUDUEUF $.
      $( [15-Oct-2012] $)

  $}

  ${
    $d x y z w $.
    $( A basis for the standard topology over the extended reals.  (Contributed
       by FL, 14-Sep-2013.)  (The proof was shortened by Mario Carneiro,
       30-Jan-2014.) $)
    basexre $p |- ( ran (,) u. { RR* } ) e. TopBases $=
      ( vz vw vx vy cioo cxr ctb wcel cv cin wss wa wrex wral ax-mp syl wceq cr
      sseq2d xrex crn csn cun retopbas isbasis2g ibi rspec wi ssun1 ssrexv elun
      ralimi wo cpw cxp wf ioof frn sseli elpwi ressxr syl6ss elsni eqimss jaoi
      sylbi df-ss sylib eleq2d biimpa simpr eqimss2 eleq2 sseq1 anbi12d rcla4ev
      adantr jca syldan ralrimiva rgen elun1 ineq1 anbi2d rexbidv raleqbidv mpi
      rcla4v wsbc ralsn ineq2 sbcie bitri sylibr ralun incom syl5eq mpbir mp2an
      syl2anc ralbidv cvv wb iooex rnex snex unex ) EUAZFUBZUCZGHZAIZBIZHZXMCIZ
      DIZJZKZLZBXJMZAXQNZDXJNZCXJNZYBCXHNYBCXINZYCYBCXHXOXHHZYADXHNZYADXINZYBYE
      XSBXHMZAXQNZDXHNZYFYJCXHXHGHZYJCXHNZUDYKYLCDABXHGUEUFOUGYIYADXHYHXTAXQXHX
      JKYHXTUHXHXIUIXSBXHXJUJOULULPYEXNXMXOFJZKZLZBXJMZAYMNZYGYEXNXMXPFJZKZLZBX
      JMZAYRNZDXJNZYQUUBDXJXPXJHZUUAAYRUUDXLYRHZXLXPHZUUAUUDUUEUUFUUDYRXPXLUUDX
      PFKZYRXPQZUUDXPXHHZXPXIHZUMUUGXPXHXIUKUUIUUGUUJUUIXPRFUUIXPRUNZHXPRKXHUUK
      XPFFUOZUUKEUPXHUUKKUQUULUUKEUROUSXPRUTPVAVBUUJXPFQZUUGXPFVCXPFVDPVEVFXPFV
      GVHZVIVJUUDUUFUUFXPYRKZLZUUAUUDUUFLUUFUUOUUDUUFVKUUDUUOUUFUUDUUHUUOUUNXPY
      RVLPVQVRYTUUPBXPXJXMXPQXNUUFYSUUOXMXPXLVMXMXPYRVNVOVPVSVSVTWAZYEXOXJHUUCY
      QUHXOXHXIWBUUBYQDXOXJXPXOQZUUAYPAYRYMXPXOFWCZUURYTYOBXJUURYSYNXNUURYRYMXM
      UUSSWDWEWFWHPWGYGYADFWIYQYADFTWJYAYQDFTUUMXTYPAXQYMXPFXOWKZUUMXSYOBXJUUMX
      RYNXNUUMXQYMXMUUTSWDWEWFWLWMWNYADXHXIWOWTWAYDUUCUUQYDYBCFWIUUCYBCFTWJYBUU
      CCFTXOFQZYAUUBDXJUVAXTUUAAXQYRUVAXQXPXOJYRXOXPWPXOFXPWKWQZUVAXSYTBXJUVAXR
      YSXNUVAXQYRXMUVBSWDWEWFXAWLWMWRYBCXHXIWOWSXJXBHXKYCXCXHXIEXDXEFXFXGCDABXJ
      XBUEOWR $.
      $( [30-Jan-2014] $) $( [14-Sep-2013] $)
  $}

  $( The standard topology over ` RR* ` .  (Contributed by FL, 15-Sep-2013.) $)
  stovr $p |- ( topGen ` ( ran (,) u. { RR* } ) ) e. Top $=
    ( cioo crn cxr csn cun ctb wcel ctg cfv ctop basexre tgcl ax-mp ) ABCDEZFGN
    HIJGKNLM $.
    $( [15-Sep-2013] $)

  ${
    cldifemp.1 $e |- X = U. J $.
    $( The closure of a class ` S ` is empty iff ` S ` is empty.  (Contributed
       by FL, 15-Sep-2013.) $)
    cldifemp $p |- ( ( J e. Top /\ S C_ X ) ->
      ( ( ( cls ` J ) ` S ) = (/) <-> S = (/) ) ) $=
      ( ctop wcel wss wa ccl cfv c0 wceq wi sscls sseq0 syl cls0 fveq2 eqeq1d
      ex syl5ibrcom adantr impbid ) BEFZACGZHZABIJZJZKLZAKLZUFAUHGZUIUJMABCDNUK
      UIUJAUHOTPUDUJUIMUEUDUIUJKUGJZKLBQUJUHULKAKUGRSUAUBUC $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d P n o $.  $d S n o $.  $d n o J $.  $d n o X $.
    neindisj2.1 $e |- X = U. J $.
    $( A point ` P ` belongs to the closure of a set ` S ` iff every
       neighborhood of ` P ` meets ` S ` .  (Contributed by FL,
       15-Sep-2013.) $)
    neindisj2 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
        ( P e. ( ( cls ` J ) ` S ) <->
          A. n e. ( ( nei ` J ) ` { P } ) ( n i^i S ) =/= (/) ) ) $=
      ( vo wcel wss w3a cfv cv cin c0 wne wi wral wa wrex ex ctop ccl csn elcls
      cnei isneip r19.29r pm3.35 ssrin sseq2 syl6bi syl5com necon3d com23 imp31
      wceq ss0 rexlimivw syl adantl 3adant2 imp ralrimiv opnneip ineq1 rcla4cva
      weq neeq1d idd 3exp com14 com3l mpcom 3expia com25 3imp1 impbida bitrd )
      DUAHZBEIZAEHZJZABDUBKKHAGLZHZWCBMZNOZPZGDQZCLZBMZNOZCAUCDUEKKZQZGABDEFUDW
      BWHWMWBWHRWKCWLWBWHWIWLHZWKPWBWNWHWKVSWAWNWHWKPZPVTVSWARWNWIEIZWDWCWIIZRZ
      GDSZRWOAGDWIEFUFWSWOWPWSWHWKWSWHRWRWGRZGDSWKWRWGGDUGWTWKGDWDWQWGWKWDWGWQW
      KWDWGWQWKPWDWGRWFWQWKWDWFUHWQWJNWENWQWEWJIZWJNUPZWENUPZWCWIBUIXBXAWENIXCW
      JNWEUJWEUQUKULUMULTUNUOURUSTUTUKVAUNVBVCWBWMRWGGDVSVTWAWMWCDHZWGPVSXDWAWM
      VTWGVSXDWAWMVTWGPPPVSXDRWDWMVTWAWFVSXDWDWMVTWAWFPPZPZWCWLHZVSXDWDJZXFADWC
      VDWMXGXHXEWMXGXHXEPZWMXGRWFXIWKWFCWCWLCGVGWJWENWIWCBVEVHVFWAXHVTWFWFWAXHV
      TWFWFPWAXHVTJWFVIVJVKUSTVLVMVNVOTVOVPVCVQVR $.
      $( [15-Sep-2013] $)
  $}

$( End of Topology $)

