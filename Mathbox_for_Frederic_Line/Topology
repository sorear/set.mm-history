$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topology
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( A topology is not empty.  (Contributed by FL, 1-Jun-2008.) $)
  topnem $p |- ( J e. Top -> J =/= (/) ) $=
    ( ctop wcel c0 wn wne 0ntop wa wceq nelneq df-ne sylibr mpan2 ) ABCZDBCEZAD
    FZGNOHADIEPADBJADKLM $.
    $( [1-Jun-2008] $)

  $( A topology includes the indiscrete topology and is included in the
     discrete topology.  It also means that when the topologies over the same
     underlying set are ordered by the inclusion, the greater element is the
     discrete topology and the smaller element is the indiscrete one.
     (Contributed by FL, 5-Jan-2009.) $)
  topindisOLD $p |- ( J e. Top -> ( { (/) , U. J } C_ J /\ J C_ ~P U. J ) ) $=
    ( ctop wcel c0 cuni cpr wss cpw wa 0opn eqid topopn jca cvv wb uniexg 0ex
    jctil prssg syl mpbid cop ctpsOLD eltopspOLD istps2OLD simplr sylbi sylbir
    ) ABCZDAEZFAGZAUJHGZUIDACZUJACZIZUKUIUMUNAJAUJUJKLMUIDNCZUJNCZIUOUKOUIUQUPA
    BPQRDUJANNSTUAUIUJAUBUCCZULAUDURUIULIUOIULUJAUEUIULUOUFUGUHM $.
    $( [5-Jan-2009] $)

  ${
    $d j J $.
    $( A topology includes the indiscrete topology and is included in the
       discrete topology.  It also means that when the topologies over the same
       underlying set are ordered by the inclusion, the greater element is the
       discrete topology and the smaller element is the indiscrete one.
       (Contributed by FL, 5-Jan-2009.) $)
    topindis $p |- ( J e. Top -> ( { (/) , U. J } C_ J /\ J C_ ~P U. J ) ) $=
      ( vj ctop wcel c0 cuni cpr wss cpw wa 0opn eqid topopn jca cvv wb uniexg
      0ex jctil prssg syl mpbid cv wceq id unieq pweq sseq12d cnx cbs cfv cop
      cts ctps eltopspg mpbird visset uniex tpsbase ax-mp tpstopset istps2
      simplr sylbi vtoclga ) ACDZEAFZGAHZAVGIZHZVFEADZVGADZJZVHVFVKVLAKAVGVGLMN
      VFEODZVGODZJVMVHPVFVOVNACQRSEVGAOOTUAUBBUCZVPFZIZHZVJBACVPAUDZVPAVRVIVTUE
      VTVQVGUDVRVIUDVPAUFVQVGUGUAUHVPCDZUIUJUKVQULUIUMUKVPULGZUNDZVSWAWCWAWAUEV
      PWBCWBLZUOUPWCWAVSJEVPDVQVPDJZJVSVQVPWBVQODVQWBUJUKUDVPBUQZURVQVPWBOWDUSU
      TVPODVPWBUMUKUDWFVQVPWBOWDVAUTVBWAVSWEVCVDUAVEN $.
      $( [21-Oct-2012] $) $( [5-Jan-2009] $)
  $}

  ${
    clsint.1 $e |- X = U. J $.
    $( The closure of an intersection is included in the intersection of the
       closures.  (Contributed by FL, 23-Feb-2009.) $)
    clsint $p |- ( ( J e. Top /\ S C_ X /\ T C_ X )
     -> ( ( cls ` J ) ` ( S i^i T ) ) C_
     ( ( ( cls ` J ) ` S ) i^i ( ( cls ` J ) ` S ) ) ) $=
      ( ctop wcel wss w3a cin ccl cfv wa inss1 a1i clsss syld3an3 jca ssin
      sylib ) CFGZADHZBDHZIZABJZCKLZLZAUFLZHZUIMUGUHUHJHUDUIUIUAUBUCUEAHZUIUJUD
      ABNOAUECDEPQZUKRUGUHUHST $.
      $( [23-Feb-2009] $)
  $}

  ${
    $d J x $.  $d P x $.  $d S x $.  $d X x $.
    islp3.1 $e |- X = U. J $.
    $( The predicate " ` P ` is a limit point of ` S ` " in terms of open sets.
       see ~ islp2 , ~ elcls , ~ islp .  (Contributed by FL, 31-Jul-2009.) $)
    islp3 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
     ( P e. ( ( limPt ` J ) ` S ) <->
        A. x e. J ( P e. x -> ( x i^i ( S \ { P } ) ) =/= (/) ) ) ) $=
      ( ctop wcel wss w3a clp cfv csn cdif ccl cv cin c0 wne wi wral wb islp
      3adant3 wa simp2 difss jctil sstr syl elcls syld3an2 bitrd ) DGHZCEIZBEHZ
      JZBCDKLLHZBCBMZNZDOLLHZBAPZHVBUTQRSTADUAZUNUOURVAUBUPBCDEFUCUDUNUTEIZUOUP
      VAVCUBUQUTCIZUOUEVDUQUOVEUNUOUPUFCUSUGUHUTCEUIUJABUTDEFUKULUM $.
      $( [31-Jul-2009] $)
  $}

  ${
    $d I u v x $.  $d J u v $.
    $( The intersection of a family of topologies is a topology.  (Contributed
       by FL, 19-Sep-2011.) $)
    inttop2 $p |- ( ( I =/= (/) /\ A. x e. I J e. Top )
      -> |^|_ x e. I J e. Top ) $=
      ( vu vv c0 wne ctop wcel wral wa ciin cv wss cuni wi wal cin uniopn ex
      ral2imi adantl ssiin cvv wb visset uniex eliin ax-mp 3imtr4g 19.21aiv
      r19.26 inopn 3expib com12 sylbir inex1 syl6ibr syl2anb r19.21aivv jca
      iinexg istopg syl mpbird ) BFGZCHIZABJZKZABCLZHIZDMZVJNZVLOZVJIZPZDQZVLEM
      ZRZVJIZEVJJDVJJZKZVIVQWAVIVPDVIVLCNZABJZVNCIZABJZVMVOVHWDWFPVFVGWCWEABVGW
      CWEVLCSTUAUBABCVLUCVNUDIVOWFUEVLDUFZUGAVNBCUDUHUIUJUKVIVTDEVJVJVLVJIZVRVJ
      IZKVIVTWHVLCIZABJZVRCIZABJZVIVTPWIVLUDIWHWKUEWGAVLBCUDUHUIVRUDIWIWMUEEUFA
      VRBCUDUHUIWKWMKZVIVSCIZABJZVTWNWJWLKZABJZVIWPPWJWLABULVIWRWPVHWRWPPVFVGWQ
      WOABVGWJWLWOVLVRCUMUNUAUBUOUPVSUDIVTWPUEVLVRWGUQAVSBCUDUHUIURUSUOUTVAVIVJ
      UDIVKWBUEABCHVBDEUDVJVCVDVE $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d J i $.
    $( The intersection of a family of topologies is a topology.  (Contributed
       by FL, 19-Sep-2011.) $)
    inttop3 $p |- ( ( J =/= (/) /\ J C_ Top )
      -> |^| J e. Top ) $=
      ( vi c0 wne ctop wss wa cint cv ciin intiin wcel wral dfss3 inttop2
      sylan2b syl5eqel ) ACDZAEFZGAHBABIZJZEBAKSRTELBAMUAELBAENBATOPQ $.
      $( [19-Sep-2011] $)
  $}

  $( The intersection of two topologies is a topology.  (Contributed by FL,
     19-Sep-2011.) $)
  inttop4 $p |- ( ( J e. Top /\ K e. Top )
      -> ( J i^i K ) e. Top ) $=
    ( ctop wcel wa cin cpr wss prssg biimpd cint wceq intprg eqcomd adantl wi
    c0 wne prnzg inttop3 ex syl adantr impcom eqeltrd syli pm2.43i ) ACDZBCDZEZ
    ABFZCDZUJUJABGZCHZULUJUJUNABCCCIJUNUJULUNUJEUKUMKZCUJUKUOLUNUJUOUKABCCMNOUJ
    UNUOCDZUHUNUPPZUIUHUMQRZUQABCSURUNUPUMTUAUBUCUDUEUAUFUG $.
    $( [19-Sep-2011] $)

  ${
    $d J x y $.  $d K x y $.
    $( The intersection of two topologies over the same underlying set ` U. J `
       is a topology over ` U. J ` . compare ~ uniin .  (Contributed by FL,
       27-Nov-2011.) $)
    unint2t $p |- ( ( J e. Top /\ K e. Top /\ U. J = U. K )
     -> U. ( J i^i K ) = U. J ) $=
      ( vx vy ctop wcel cuni wceq w3a cin wss uniin inss1 a1i sstr sylancr cv
      wrex wral wi eqid topopn wb eleq1 eqcoms wa elin biimpri expcom syl6bi
      com3l syl syl5com 3imp elssuni sseq2 rcla4ev syl2an r19.21aiva uniss2
      eqssd ) AEFZBEFZAGZBGZHZIZABJZGZVDVGVIVDVEJZKVJVDKZVIVDKABLVKVGVDVEMNVIVJ
      VDOPVGCQZDQZKZDVHRZCASVDVIKVGVOCAVGVDVHFZVLVDKZVOVLAFVBVCVFVPVBVDAFZVCVFV
      PTZAVDVDUAUBVCVEBFZVRVSTBVEVEUAUBVFVTVRVPVFVTVDBFZVRVPTVTWAUCVEVDVEVDBUDU
      EVRWAVPVPVRWAUFVDABUGUHUIUJUKULUMUNVLAUOVNVQDVDVHVMVDVLUPUQURUSCDAVHUTULV
      A $.
      $( [27-Nov-2011] $)
  $}

  ${
    $d J a b $.  $d J a x $.  $d T a b $.  $d T a x $.
    $( The intersection of a family of topologies over the same underying set
       ` U. J ` is a topology over ` U. J ` .  (Contributed by FL,
       27-Nov-2011.) $)
    intfmu2 $p |- ( ( T C_ Top
      /\ J e. T /\ A. x e. T U. x = U. J )
         -> U. |^| T = U. J ) $=
      ( va vb ctop wss wcel cv cuni wceq wral w3a cint intss1 3ad2ant2 uniss
      syl wrex wa wi ssel eqid topopn syl6 adantr imp wb eleq1 eqcoms
      syl5ibrcom ralimdva 3impia cvv uniexg elintg mpbird elssuni sseq2
      rcla4ev syl2an r19.21aiva uniss2 eqssd ) BFGZCBHZAIZJZCJZKZABLZMZBNZJZVIV
      LVMCGZVNVIGVFVEVOVKCBOPVMCQRVLDIZEIZGZEVMSZDCLVIVNGVLVSDCVLVIVMHZVPVIGZVS
      VPCHVLVTVIVGHZABLZVEVFVKWCVEVFTZVJWBABWDVGBHZTWBVJVHVGHZWDWEWFVEWEWFUAVFV
      EWEVGFHWFBFVGUBVGVHVHUCUDUEUFUGWBWFUHVIVHVIVHVGUIUJUKULUMVLVIUNHZVTWCUHVF
      VEWGVKCBUOPAVIBUNUPRUQVPCURVRWAEVIVMVQVIVPUSUTVAVBDECVMVCRVD $.
      $( [27-Nov-2011] $)
  $}

  ${
    $d A a $.  $d J a $.
    $( The intersection of a non-empty finite family of open sets is open.
       (Contributed by FL, 20-Apr-2012.) $)
    neffopo $p |- ( J e. Top -> ( ( A C_ J /\ A =/= (/) /\ A e. Fin )
        -> |^| A e. J ) ) $=
      ( va wss c0 wne cfn wcel w3a ctop cint wi cpw elpwg cv wceq sseq1 neeq1
      eleq1 3anbi123d inteq eleq1d imbi2d imbi12d cuni wal wa istop2g ibi ax4
      adantl syl com12 vtoclg 3exp com3r com4r syl6bir pm2.43a com4l pm2.43i
      3imp ) ABDZAEFZAGHZIZBJHZAKZBHZVCVDVEVGVILZVCVDVEVJLLVEVCVCVDVJVCVEVCVDVJ
      LLZVEVCABMZHZVEVKLABGNVEVCVDVMVJVCVDVEVMVJLZVCVDVEVNVMVFVJCOZBDZVOEFZVOGH
      ZIZVGVOKZBHZLZLVFVJLCAVLVOAPZVSVFWBVJWCVPVCVQVDVRVEVOABQVOAERVOAGSTWCWAVI
      VGWCVTVHBVOAUAUBUCUDVGVSWAVGVPVOUEBHLCUFZVSWALZCUFZUGZWEVGWGCJBUHUIWFWEWD
      WECUJUKULUMUNUMUOUPUQURUSUTVAVBUM $.
      $( [20-Apr-2012] $)
  $}

  ${
    $( If a subbase is included into a topology, so is the generated topology.
       (Contributed by FL, 20-Apr-2012.) $)
    sbincgt $p |- ( ( J e. Top /\ A C_ J )
      -> ( topGen ` ( fi ` A ) ) C_ J ) $=
      ( ctop wcel wss cfi cfv ctg wceq wi tgtop ctb w3a wa cvv ssexg ancoms
      fibas syl topbas adantr fitop fiss ex sseq2 imbi2d syl5ibr eqcoms mpcom
      imp 3jca tgss syl6 wb ) BCDZABEZAFGZHGZBEZBHGZBIZUOUPUSJZBKUOVBVAUPURUTEZ
      JUOUPUQLDZBLDZUQBEZMZVCUOUPVGUOUPNZVDVEVFVHAODZVDUPUOVIABCPQAORSUOVEUPBTU
      AUOUPVFBFGZBIUOUPVFJZBUBUOVKJBVJUOVKBVJIZUPUQVJEZJUOUPVMABCUCUDVLVFVMUPBV
      JUQUEUFUGUHUIUJUKUDUQBULUMVAUSVCUPUSVCUNBUTBUTURUEUHUFUGUIUJ $.
      $( [20-Apr-2012] $)
  $}

  ${
    $d A v $.  $d J v $.  $d X v $.
    apnei.1 $e |- X = U. J $.
    $( Any point has a neighborhood.  (Contributed by FL, 15-Oct-2012.) $)
    apnei $p |- ( ( J e. Top /\ A e. X ) -> E. v v e. ( ( nei ` J ) ` { A } ) )
        $=
      ( ctop wcel wa cv csn cnei cfv wrex wex wss snssi tpnei syl5ibcom impcom
      eleq1 rcla4ev syl11anc df-rex anidm exbii bitri sylib ) CFGZBDGZHZAIZBJZC
      KLLZGZAUMMZUNANZUJDUMGZUQUOUIUHUQUIULDOUHUQBDPULCDEQRSZURUNUQADUMUKDUMTUA
      UBUOUNUNHZANUPUNAUMUCUSUNAUNUDUEUFUG $.
      $( [15-Oct-2012] $)
  $}

  ${
    $( A neighborhood of a point can't be empty.  (Contributed by FL,
       15-Oct-2012.) $)
    npmp $p |- ( ( J e. Top /\ A e. X /\ N e. ( ( nei ` J ) ` { A } ) )
     -> N =/= (/) ) $=
      ( ctop wcel csn cnei cfv w3a c0 wne wa wn wi simp1 snnzg 3ad2ant2 jca
      0nnei nelne2 ex 3ad2ant3 com12 3syl pm2.43i ) BEFZADFZCAGZBHIIZFZJZCKLZUL
      UGUIKLZMKUJFNZULUMOULUGUNUGUHUKPUHUGUNUKADQRSUIBTULUOUMUKUGUOUMOUHUKUOUMC
      KUJUAUBUCUDUEUF $.
      $( [15-Oct-2012] $)

  $}

  ${
    $d x y z w v u f g t e $.
    $( A basis for the standard topology over the extended reals.  (Contributed
       by FL, 14-Sep-2013.) $)
    basexre $p |- ( ran (,) u. { RR* } ) e. TopBases $=
      ( vz vw vx vy vt ve vf vg vv vu cioo crn cxr csn cun ctb wcel wel cv cin
      wss wa wrex wral wo wi elun co wceq cle wbr cif ifcl ad2ant2r adantr
      ancoms ad2ant2l iooin ineq12 sylan9req opreq1 eqeq1d opreq2 rcla42ev
      syl111anc exp32 exp31 com4t imp31 r19.23adv r19.23adva r19.23aiv imp
      ancom cxp cr cpw wf wfn wb ioof ffn oprvelrn anbi12d mp2b bitri 3imtr4i
      orcd elsn cuni unirnioo elssuni ressxr sseq2 biimpd sstr df-ss eleq1
      biimprd sylbi syl expcom ee02 mpsyl pm2.43i ineq2 eleq1d syl5ibr incom
      eqtr mpan ineq1 impcom visset elsnc inidm syl6eq inex1 sylibr olcd ccase
      ex com12 ssid jctr eleq2 sseq1 rcla4ev syl2an 3impa rgen3 cvv iooex rnex
      snexALT unex isbasis2g ax-mp mpbir ) KLZMNZOZPQZABRZBSZCSZDSZTZUAZUBZBUUB
      UCZAUUHUDDUUBUDCUUBUDZUUKCDAUUBUUBUUHUUFUUBQZUUGUUBQZASZUUHQZUUKUUMUUNUBU
      UHUUBQZUUPUUHUUHUAZUBZUUKUUPUUMUUNUUQUUMUUFYTQZUUFUUAQZUEZUUNUUQUFUUFYTUU
      AUGUUNUVBUUQUUNUUGYTQZUUGUUAQZUEZUVBUUQUFUUGYTUUAUGUVEUVBUUQUVEUVBUBUUHYT
      QZUUHUUAQZUEZUUQUVCUUTUVDUVAUVHUVCUUTUBZUVFUVGESZFSZKUHZUUFUIZFMUCZEMUCZG
      SZHSZKUHZUUGUIZHMUCZGMUCZUBZISZJSZKUHZUUHUIZJMUCIMUCZUVIUVFUVOUWAUWGUVNUW
      AUWGUFZEMUVJMQZUVMUWHFMUWIUVKMQZUVMUWHUWIUWJUBZUVMUBZUVTUWGGMUWLUVPMQZUBU
      VSUWGHMUWKUVMUWMUVQMQZUVSUWGUFZUFUWMUWNUWKUVMUWOUWMUWNUWKUVMUWOUFUWMUWNUB
      ZUWKUBZUVMUVSUWGUWQUVMUVSUBZUBUVJUVPUJUKZUVPUVJULZMQZUVKUVQUJUKZUVKUVQULZ
      MQZUWTUXCKUHZUUHUIZUWGUWQUXAUWRUWMUWIUXAUWNUWJUWSUVPUVJMUMUNUOUWQUXDUWRUW
      NUWJUXDUWMUWIUWJUWNUXDUXBUVKUVQMUMUPUQUOUWQUWRUXEUVLUVRTZUUHUWKUWPUXGUXEU
      IUVJUVKUVPUVQURUPUVLUUFUVRUUGUSUTUWFUXFUWTUWDKUHZUUHUIIJUWTUXCMMUWCUWTUIU
      WEUXHUUHUWCUWTUWDKVAVBUWDUXCUIUXHUXEUUHUWDUXCUWTKVCVBVDVEVFVGVHVIVJVKVGVJ
      VLVMUVIUUTUVCUBZUWBUVCUUTVNMMVOZVPVQZKVRZKUXJVSZUXIUWBVTWAUXJUXKKWBZUXMUU
      TUVOUVCUWAEFMMUUFKWCGHMMUUGKWCWDWEWFUXLUXMUVFUWGVTWAUXNIJMMUUHKWCWEWGWHUV
      DUUTUBUVFUVGUVDUUTUVFUVDUUGMUIZUUTUVFUFDMWIUUTUVFUXOUUFMTZYTQZUUTUXQYTWJZ
      VPUIZUUTUUFUXRUAZUUTUXQUFZWKUUFYTWLVPMUAZUXSUXTUUFVPUAZUYAWMUXSUXTUYCUXRV
      PUUFWNWOUYCUYBUYAUYCUYBUBUUFMUAZUYAUUFVPMWPUYDUXPUUFUIZUYAUUFMWQUYEUXQUUT
      UXPUUFYTWRWSWTXAXBXCXDXEUXOUUHUXPYTUUGMUUFXFZXGXHWTVMWHUVCUVAUBUVFUVGUVAU
      VCUVFUVAUUFMUIZUVCUVFUFCMWIUVCUVFUYGMUUGTZYTQZUVCUYIUXSUVCUUGUXRUAZUVCUYI
      UFZWKUUGYTWLUYBUXSUYJUUGVPUAZUYKWMUXSUYJUYLUXRVPUUGWNWOUYLUYBUYKUYLUYBUBU
      UGMUAZUYKUUGVPMWPUYMUUGMTZUUGUIZUYKUUGMWQUYOUYIUVCUYHUYNUIZUYOUYIUVCVTMUU
      GXIUYPUYOUBUYHUUGYTUYHUYNUUGXJXGXKWSWTXAXBXCXDXEUYGUUHUYHYTUUFMUUGXLXGXHW
      TXMWHUVDUVAUBZUVGUVFUYQUUHMUIZUVGUVDUVAUYRUVDUXOUVAUYRUFUUGMDXNXOUVAUYRUX
      OUXPMUIZUVAUYGUYSUUFMCXNZXOUYGUXPMMTMUUFMMXLMXPXQWTUXOUUHUXPMUYFVBXHWTVMU
      UHMUUFUUGUYTXRXOXSXTYAUUHYTUUAUGXSYBWTYCWTVMUUPUURUUHYDYEUUJUUSBUUHUUBUUE
      UUHUIUUDUUPUUIUURUUEUUHUUOYFUUEUUHUUHYGWDYHYIYJYKUUBYLQUUCUULVTYTUUAKYMYN
      MYOYPCDABUUBYLYQYRYS $.
      $( [14-Sep-2013] $)
  $}

  $( The standard topology over ` RR* ` .  (Contributed by FL, 15-Sep-2013.) $)
  stovr $p |- ( topGen ` ( ran (,) u. { RR* } ) ) e. Top $=
    ( cioo crn cxr csn cun ctb wcel ctg cfv ctop basexre tgcl ax-mp ) ABCDEZFGN
    HIJGKNLM $.
    $( [15-Sep-2013] $)

  ${
    cldifemp.1 $e |- X = U. J $.
    $( The closure of a class ` S ` is empty iff ` S ` is empty.  (Contributed
       by FL, 15-Sep-2013.) $)
    cldifemp $p |- ( ( J e. Top /\ S C_ X ) ->
      ( ( ( cls ` J ) ` S ) = (/) <-> S = (/) ) ) $=
      ( ctop wcel wss wa ccl cfv c0 wceq wi sscls sseq0 ex syl cls0 fveq2
      eqeq1d syl5ibrcom adantr impbid ) BEFZACGZHZABIJZJZKLZAKLZUFAUHGZUIUJMABC
      DNUKUIUJAUHOPQUDUJUIMUEUDUIUJKUGJZKLBRUJUHULKAKUGSTUAUBUC $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d P n o $.  $d S n o $.  $d n o J $.  $d n o X $.
    neindisj2.1 $e |- X = U. J $.
    $( A point ` P ` belongs to the closure of a set ` S ` iff every
       neighborhood of ` P ` meets ` S ` .  (Contributed by FL,
       15-Sep-2013.) $)
    neindisj2 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
        ( P e. ( ( cls ` J ) ` S ) <->
          A. n e. ( ( nei ` J ) ` { P } ) ( n i^i S ) =/= (/) ) ) $=
      ( vo ctop wcel wss w3a ccl cfv cv cin c0 wne wi wral csn cnei elcls wa
      wrex isneip r19.29r pm3.35 wceq ssrin sseq2 ss0 syl6bi syl5com necon3d
      ex com23 imp31 r19.23aivw syl adantl 3adant2 imp r19.21aiv opnneip weq
      ineq1 neeq1d rcla4cva idd 3exp com14 com3l mpcom 3expia com25 3imp1
      impbida bitrd ) DHIZBEJZAEIZKZABDLMMIAGNZIZWCBOZPQZRZGDSZCNZBOZPQZCATDUAM
      MZSZGABDEFUBWBWHWMWBWHUCWKCWLWBWHWIWLIZWKRWBWNWHWKVSWAWNWHWKRZRVTVSWAUCWN
      WIEJZWDWCWIJZUCZGDUDZUCWOAGDWIEFUEWSWOWPWSWHWKWSWHUCWRWGUCZGDUDWKWRWGGDUF
      WTWKGDWDWQWGWKWDWGWQWKWDWGWQWKRWDWGUCWFWQWKWDWFUGWQWJPWEPWQWEWJJZWJPUHZWE
      PUHZWCWIBUIXBXAWEPJXCWJPWEUJWEUKULUMUNUMUOUPUQURUSUOUTULVAUPVBVCWBWMUCWGG
      DVSVTWAWMWCDIZWGRVSXDWAWMVTWGVSXDWAWMVTWGRRRVSXDUCWDWMVTWAWFVSXDWDWMVTWAW
      FRRZRZWCWLIZVSXDWDKZXFADWCVDWMXGXHXEWMXGXHXERZWMXGUCWFXIWKWFCWCWLCGVEWJWE
      PWIWCBVFVGVHWAXHVTWFWFWAXHVTWFWFRWAXHVTKWFVIVJVKUSUOVLVMVNVOUOVOVPVCVQVR
      $.
      $( [15-Sep-2013] $)
  $}


$( End of Topology $)

