$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topology
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( The empty set is not a topology. $)
  empntop $p |- -. (/) e. Top $=
    ( c0 ctop wcel noel 0opn mto ) ABCAACADAEF $.
    $( [1-Jun-2008] $)

  $( A topology is not empty. $)
  topnem $p |- ( J e. Top -> J =/= (/) ) $=
    ( ctop wcel c0 wn wne empntop wa wceq nelneq df-ne sylibr mpan2 ) ABCZDBCEZ
    ADFZGNOHADIEPADBJADKLM $.
    $( [1-Jun-2008] $)

  $( A topology includes the indiscrete topology and is included in the
     discrete topology.  It also means that when the topologies over the same
     underlying set are ordered by the inclusion, the greater element is the
     discrete topology and the smaller element is the indiscrete one. $)
  topindis $p |- ( J e. Top -> ( { (/) , U. J } C_ J /\ J C_ ~P U. J ) ) $=
    ( ctop wcel c0 cuni cpr wss cpw wa 0opn eqid topopn jca cvv wb uniexg 0ex
    jctil prssg syl mpbid cop ctps eltopsp istps2 simplr sylbi sylbir ) ABCZDAE
    ZFAGZAUJHGZUIDACZUJACZIZUKUIUMUNAJAUJUJKLMUIDNCZUJNCZIUOUKOUIUQUPABPQRDUJAN
    NSTUAUIUJAUBUCCZULAUDURUIULIUOIULUJAUEUIULUOUFUGUHM $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)

  ${
    clsint.1 $e |- X = U. J $.
    $( The closure of an intersection is included in the intersection of the
       closures. $)
    clsint $p |- ( ( J e. Top /\ S C_ X /\ T C_ X )
     -> ( ( cls ` J ) ` ( S i^i T ) ) C_
     ( ( ( cls ` J ) ` S ) i^i ( ( cls ` J ) ` S ) ) ) $=
      ( ctop wcel wss w3a cin ccl cfv wa clsss inss1 a1i syld3an3 jca ssin
      sylib ) CFGZADHZBDHZIZABJZCKLZLZAUFLZHZUIMUGUHUHJHUDUIUIUAUBUEAHZUIUCAUEC
      DENUJUDABOPQZUKRUGUHUHST $.
      $( [3-Aug-2009] $) $( [23-Feb-2009] $)
  $}

  ${
    $d J x $.  $d P x $.  $d S x $.  $d X x $.
    islp3.1 $e |- X = U. J $.
    $( The predicate " ` P ` is a limit point of ` S ` " in terms of open sets.
       see ~ islp2 , ~ elcls , ~ islp . $)
    islp3 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
     ( P e. ( ( limPt ` J ) ` S ) <->
        A. x e. J ( P e. x -> ( x i^i ( S \ { P } ) ) =/= (/) ) ) ) $=
      ( ctop wcel wss w3a clp cfv csn cdif ccl cv cin c0 wne wi wral wb islp
      3adant3 elcls wa simp2 difss jctil sstr syl syld3an2 bitrd ) DGHZCEIZBEHZ
      JZBCDKLLHZBCBMZNZDOLLHZBAPZHVBUTQRSTADUAZUNUOURVAUBUPBCDEFUCUDUNUTEIZUPVA
      VCUBUOABUTDEFUEUQUTCIZUOUFVDUQUOVEUNUOUPUGCUSUHUIUTCEUJUKULUM $.
      $( [3-Aug-2009] $) $( [31-Jul-2009] $)
  $}

  ${
    $d I u v x $.  $d J u v $.
    $( The intersection of a family of topologies is a topology. $)
    inttop2 $p |- ( ( I =/= (/) /\ A. x e. I J e. Top )
      -> |^|_ x e. I J e. Top ) $=
      ( vu vv c0 wne ctop wcel wral wa ciin cv wss cuni wi wal cin uniopn ex
      ral2imi adantl ssiin cvv wb visset uniex eliin ax-mp 3imtr4g 19.21aiv
      r19.26 inopn 3expib com12 sylbir inex1 syl6ibr syl2anb r19.21aivv jca
      cexint2 istopg syl mpbird ) BFGZCHIZABJZKZABCLZHIZDMZVJNZVLOZVJIZPZDQZVLE
      MZRZVJIZEVJJDVJJZKZVIVQWAVIVPDVIVLCNZABJZVNCIZABJZVMVOVHWDWFPVFVGWCWEABVG
      WCWEVLCSTUAUBABCVLUCVNUDIVOWFUEVLDUFZUGAVNBCUDUHUIUJUKVIVTDEVJVJVLVJIZVRV
      JIZKVIVTVLCIZABJZVRCIZABJZVIVTPWHWIWKWMKZVIVSCIZABJZVTWNWJWLKZABJZVIWPPWJ
      WLABULVIWRWPVHWRWPPVFVGWQWOABVGWJWLWOVLVRCUMUNUAUBUOUPVSUDIVTWPUEVLVRWGUQ
      AVSBCUDUHUIURVLUDIWHWKUEWGAVLBCUDUHUIVRUDIWIWMUEEUFAVRBCUDUHUIUSUOUTVAVIV
      JUDIVKWBUEABCHVBDEUDVJVCVDVE $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d J i $.
    $( The intersection of a family of topologies is a topology. $)
    inttop3 $p |- ( ( J =/= (/) /\ J C_ Top )
      -> |^| J e. Top ) $=
      ( vi c0 wne ctop wss wa cv ciin cint wcel wral inttop2 dfss3 sylan2b
      intiin syl5eqel ) ACDZAEFZGBABHZIZEAJRTEKBALUAEKSBATMBAENOBAPQ $.
      $( [19-Sep-2011] $)
  $}

  $( The intersection of two topologies is a topology. $)
  inttop4 $p |- ( ( J e. Top /\ K e. Top )
      -> ( J i^i K ) e. Top ) $=
    ( ctop wcel wa cin cpr wss prssg biimpd cint wceq intprg eqcomd adantl wi
    c0 wne prnzg inttop3 ex syl adantr impcom eqeltrd syli pm2.43i ) ACDZBCDZEZ
    ABFZCDZUJUJABGZCHZULUJUJUNABCCCIJUNUJULUNUJEUKUMKZCUJUKUOLUNUJUOUKABCCMNOUJ
    UNUOCDZUHUNUPPZUIUHUMQRZUQABCSURUNUPUMTUAUBUCUDUEUAUFUG $.
    $( [19-Sep-2011] $)

  ${
    $d J x y $.  $d K x y $.
    $( The intersection of two topologies over the same underlying set ` U. J `
       is a topology over ` U. J ` . compare ~ uniin . $)
    unint2t $p |- ( ( J e. Top /\ K e. Top /\ U. J = U. K )
     -> U. ( J i^i K ) = U. J ) $=
      ( vx vy ctop wcel cuni wceq w3a cin wss sstr uniin inss1 a1i sylancr cv
      wrex wral sseq2 rcla4ev wi eqid topopn wb eleq1 eqcoms wa elin biimpri
      expcom syl6bi com3l syl syl5com 3imp elssuni syl2an r19.21aiva uniss2
      eqssd ) AEFZBEFZAGZBGZHZIZABJZGZVDVIVDVEJZKVJVDKZVIVDKVGVIVJVDLABMVKVGVDV
      ENOPVGCQZDQZKZDVHRZCASVDVIKVGVOCAVDVHFZVLVDKZVOVGVLAFVNVQDVDVHVMVDVLTUAVB
      VCVFVPVCVDAFZVFVPUBZVBVCVEBFZVRVSUBBVEVEUCUDVFVTVRVPVFVTVDBFZVRVPUBVTWAUE
      VEVDVEVDBUFUGVRWAVPVPVRWAUHVDABUIUJUKULUMUNAVDVDUCUDUOUPVLAUQURUSCDAVHUTU
      NVA $.
      $( [16-Apr-2012] $) $( [27-Nov-2011] $)
  $}

  ${
    $d J a b $.  $d J a x $.  $d T a b $.  $d T a x $.
    $( The intersection of a family of topologies over the same underying set
       ` U. J ` is a topology over ` U. J ` . $)
    intfmu2 $p |- ( ( T C_ Top
      /\ J e. T /\ A. x e. T U. x = U. J )
         -> U. |^| T = U. J ) $=
      ( va vb ctop wss wcel cv cuni wceq wral w3a cint intss1 3ad2ant2 uniss
      syl wrex sseq2 rcla4ev wa wb eleq1 eqcoms wi ssel eqid topopn syl6
      adantr imp syl5cbir ralimdvaa 3impia cvv uniexg elintg mpbird elssuni
      syl2an r19.21aiva uniss2 eqssd ) BFGZCBHZAIZJZCJZKZABLZMZBNZJZVIVLVMCGZVN
      VIGVFVEVOVKCBOPVMCQRVLDIZEIZGZEVMSZDCLVIVNGVLVSDCVIVMHZVPVIGZVSVLVPCHVRWA
      EVIVMVQVIVPTUAVLVTVIVGHZABLZVEVFVKWCVEVFUBZVJWBABVJWBVHVGHZWDVGBHZUBWBWEU
      CVIVHVIVHVGUDUEWDWFWEVEWFWEUFVFVEWFVGFHWEBFVGUGVGVHVHUHUIUJUKULUMUNUOVLVI
      UPHZVTWCUCVFVEWGVKCBUQPAVIBUPURRUSVPCUTVAVBDECVMVCRVD $.
      $( [16-Apr-2012] $) $( [27-Nov-2011] $)
  $}

  ${
    $d A a $.  $d J a $.
    $( The intersection of a non-empty finite family of open sets is open. $)
    neffopo $p |- ( J e. Top -> ( ( A C_ J /\ A =/= (/) /\ A e. Fin )
        -> |^| A e. J ) ) $=
      ( va wss c0 wne cfn wcel w3a ctop cint wi cpw elpwg cv wceq sseq1 neeq1
      eleq1 3anbi123d inteq eleq1d imbi2d imbi12d cuni wal wa istop2g ibi ax4
      adantl syl com12 vtoclg 3exp com3r com4r syl6bir pm2.43a com4l pm2.43i
      3imp ) ABDZAEFZAGHZIZBJHZAKZBHZVCVDVEVGVILZVCVDVEVJLLVEVCVCVDVJVCVEVCVDVJ
      LLZVEVCABMZHZVEVKLABGNVEVCVDVMVJVCVDVEVMVJLZVCVDVEVNVMVFVJCOZBDZVOEFZVOGH
      ZIZVGVOKZBHZLZLVFVJLCAVLVOAPZVSVFWBVJWCVPVCVQVDVRVEVOABQVOAERVOAGSTWCWAVI
      VGWCVTVHBVOAUAUBUCUDVGVSWAVGVPVOUEBHLCUFZVSWALZCUFZUGZWEVGWGCJBUHUIWFWEWD
      WECUJUKULUMUNUMUOUPUQURUSUTVAVBUM $.
      $( [21-May-2012] $) $( [20-Apr-2012] $)
  $}

  ${
    $( If a subbase is included into a topology, so is the generated
       topology. $)
    sbincgt $p |- ( ( J e. Top /\ A C_ J )
      -> ( topGen ` ( fi ` A ) ) C_ J ) $=
      ( ctop wcel wss cfi cfv ctg wceq wi tgtop wb sseq2 eqcoms imbi2d ctb w3a
      wa cvv ssexg ancoms fibas syl topbas adantr fitop fiss ex syl5bir mpcom
      imp 3jca tgss syl6 ) BCDZABEZAFGZHGZBEZBHGZBIZUOUPUSJZBKVAVBUPURUTEZJUOVA
      USVCUPUSVCLBUTBUTURMNOUOUPUQPDZBPDZUQBEZQZVCUOUPVGUOUPRZVDVEVFVHASDZVDUPU
      OVIABCTUAASUBUCUOVEUPBUDUEUOUPVFBFGZBIUOUPVFJZBUFUOVKJBVJBVJIZVKUPUQVJEZJ
      UOVLVFVMUPBVJUQMOUOUPVMABCUGUHUINUJUKULUHUQBUMUNUIUJUK $.
      $( [21-May-2012] $) $( [20-Apr-2012] $)
  $}

  ${
    $d A v $.  $d J v $.  $d X v $.
    apnei.1 $e |- X = U. J $.
    $( Any point has a neighborhood. $)
    apnei $p |- ( ( J e. Top /\ A e. X ) -> E. v v e. ( ( nei ` J ) ` { A } ) )
        $=
      ( ctop wcel wa cv csn cnei cfv wrex wex wss tpnei snssi syl5cbi impcom
      eleq1 rcla4ev syl11anc df-rex anidm exbii bitri sylib ) CFGZBDGZHZAIZBJZC
      KLLZGZAUMMZUNANZUJDUMGZUQUOUIUHUQUHULDOUQUIULCDEPBDQRSZURUNUQADUMUKDUMTUA
      UBUOUNUNHZANUPUNAUMUCUSUNAUNUDUEUFUG $.
      $( [15-Oct-2012] $)
  $}

  ${
    $( A neighborhood of a point can't be empty. $)
    npmp $p |- ( ( J e. Top /\ A e. X /\ N e. ( ( nei ` J ) ` { A } ) )
     -> N =/= (/) ) $=
      ( ctop wcel csn cnei cfv w3a c0 wne wa wn wi simp1 snnzg 3ad2ant2 jca
      0nnei nelne2 ex 3ad2ant3 com12 3syl pm2.43i ) BEFZADFZCAGZBHIIZFZJZCKLZUL
      UGUIKLZMKUJFNZULUMOULUGUNUGUHUKPUHUGUNUKADQRSUIBTULUOUMUKUGUOUMOUHUKUOUMC
      KUJUAUBUCUDUEUF $.
      $( [15-Oct-2012] $)

  $}

$( End of Topology $)

