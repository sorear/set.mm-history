$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topology
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( A topology is not empty.  (Contributed by FL, 1-Jun-2008.) $)
  topnem $p |- ( J e. Top -> J =/= (/) ) $=
    ( ctop wcel c0 wn wne 0ntop wa wceq nelneq df-ne sylibr mpan2 ) ABCZDBCEZAD
    FZGNOHADIEPADBJADKLM $.
    $( [1-Jun-2008] $)

  ${
    clsint.1 $e |- X = U. J $.
    $( The closure of an intersection is included in the intersection of the
       closures.  (Contributed by FL, 23-Feb-2009.) $)
    clsint $p |- ( ( J e. Top /\ S C_ X /\ T C_ X )
     -> ( ( cls ` J ) ` ( S i^i T ) ) C_
     ( ( ( cls ` J ) ` S ) i^i ( ( cls ` J ) ` S ) ) ) $=
      ( ctop wcel wss w3a cin ccl cfv inss1 a1i clsss syld3an3 jca ssin sylib
      wa ) CFGZADHZBDHZIZABJZCKLZLZAUFLZHZUITUGUHUHJHUDUIUIUAUBUCUEAHZUIUJUDABM
      NAUECDEOPZUKQUGUHUHRS $.
      $( [23-Feb-2009] $)
  $}

  ${
    $d J x $.  $d P x $.  $d S x $.  $d X x $.
    islp3.1 $e |- X = U. J $.
    $( The predicate " ` P ` is a limit point of ` S ` " in terms of open sets.
       see ~ islp2 , ~ elcls , ~ islp .  (Contributed by FL, 31-Jul-2009.) $)
    islp3 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
     ( P e. ( ( limPt ` J ) ` S ) <->
        A. x e. J ( P e. x -> ( x i^i ( S \ { P } ) ) =/= (/) ) ) ) $=
      ( ctop wcel wss w3a clp cfv csn cdif ccl cv cin c0 wne wb wi wral 3adant3
      islp wa simp2 difss jctil sstr syl elcls syld3an2 bitrd ) DGHZCEIZBEHZJZB
      CDKLLHZBCBMZNZDOLLHZBAPZHVBUTQRSUAADUBZUNUOURVATUPBCDEFUDUCUNUTEIZUOUPVAV
      CTUQUTCIZUOUEVDUQUOVEUNUOUPUFCUSUGUHUTCEUIUJABUTDEFUKULUM $.
      $( [31-Jul-2009] $)
  $}

  ${
    $d I u v x $.  $d J u v $.
    $( The intersection of a family of topologies is a topology.  (Contributed
       by FL, 19-Sep-2011.) $)
    inttop2 $p |- ( ( I =/= (/) /\ A. x e. I J e. Top )
      -> |^|_ x e. I J e. Top ) $=
      ( vu vv ctop wcel wral wa cv wss wi ral2imi adantl cvv visset eliin ax-mp
      wb com12 c0 wne ciin cuni wal cin uniopn ex ssiin 3imtr4g 19.21aiv r19.26
      uniex inopn 3expib sylbir inex1 syl6ibr syl2anb r19.21aivv jca iinexg syl
      istopg mpbird ) BUAUBZCFGZABHZIZABCUCZFGZDJZVJKZVLUDZVJGZLZDUEZVLEJZUFZVJ
      GZEVJHDVJHZIZVIVQWAVIVPDVIVLCKZABHZVNCGZABHZVMVOVHWDWFLVFVGWCWEABVGWCWEVL
      CUGUHMNABCVLUIVNOGVOWFSVLDPZUMAVNBCOQRUJUKVIVTDEVJVJVLVJGZVRVJGZIVIVTWHVL
      CGZABHZVRCGZABHZVIVTLWIVLOGWHWKSWGAVLBCOQRVROGWIWMSEPAVRBCOQRWKWMIZVIVSCG
      ZABHZVTWNWJWLIZABHZVIWPLWJWLABULVIWRWPVHWRWPLVFVGWQWOABVGWJWLWOVLVRCUNUOM
      NTUPVSOGVTWPSVLVRWGUQAVSBCOQRURUSTUTVAVIVJOGVKWBSABCFVBDEOVJVDVCVE $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d J i $.
    $( The intersection of a family of topologies is a topology.  (Contributed
       by FL, 19-Sep-2011.) $)
    inttop3 $p |- ( ( J =/= (/) /\ J C_ Top )
      -> |^| J e. Top ) $=
      ( vi c0 wne ctop wss wa cint ciin intiin wcel wral dfss3 inttop2 syl5eqel
      cv sylan2b ) ACDZAEFZGAHBABPZIZEBAJSRTEKBALUAEKBAEMBATNQO $.
      $( [19-Sep-2011] $)
  $}

  $( The intersection of two topologies is a topology.  (Contributed by FL,
     19-Sep-2011.) $)
  inttop4 $p |- ( ( J e. Top /\ K e. Top )
      -> ( J i^i K ) e. Top ) $=
    ( ctop wcel wa cin cpr wss prssg biimpd cint intprg eqcomd adantl wi c0 wne
    wceq prnzg ex inttop3 syl adantr impcom eqeltrd syli pm2.43i ) ACDZBCDZEZAB
    FZCDZUJUJABGZCHZULUJUJUNABCCCIJUNUJULUNUJEUKUMKZCUJUKUORUNUJUOUKABCCLMNUJUN
    UOCDZUHUNUPOZUIUHUMPQZUQABCSURUNUPUMUATUBUCUDUETUFUG $.
    $( [19-Sep-2011] $)

  ${
    $d J x y $.  $d K x y $.
    $( The intersection of two topologies over the same underlying set ` U. J `
       is a topology over ` U. J ` . compare ~ uniin .  (Contributed by FL,
       27-Nov-2011.) $)
    unint2t $p |- ( ( J e. Top /\ K e. Top /\ U. J = U. K )
     -> U. ( J i^i K ) = U. J ) $=
      ( vx vy ctop wcel cuni wceq w3a cin wss uniin inss1 a1i sstr cv wi topopn
      eqid syl sylancr wrex wral wb eleq1 eqcoms wa biimpri expcom syl6bi com3l
      elin syl5com 3imp elssuni sseq2 rcla4ev syl2an r19.21aiva uniss2 eqssd )
      AEFZBEFZAGZBGZHZIZABJZGZVDVGVIVDVEJZKVJVDKZVIVDKABLVKVGVDVEMNVIVJVDOUAVGC
      PZDPZKZDVHUBZCAUCVDVIKVGVOCAVGVDVHFZVLVDKZVOVLAFVBVCVFVPVBVDAFZVCVFVPQZAV
      DVDSRVCVEBFZVRVSQBVEVESRVFVTVRVPVFVTVDBFZVRVPQVTWAUDVEVDVEVDBUEUFVRWAVPVP
      VRWAUGVDABULUHUIUJUKTUMUNVLAUOVNVQDVDVHVMVDVLUPUQURUSCDAVHUTTVA $.
      $( [27-Nov-2011] $)
  $}

  ${
    $d J a b $.  $d J a x $.  $d T a b $.  $d T a x $.
    $( The intersection of a family of topologies over the same underying set
       ` U. J ` is a topology over ` U. J ` .  (Contributed by FL,
       27-Nov-2011.) $)
    intfmu2 $p |- ( ( T C_ Top
      /\ J e. T /\ A. x e. T U. x = U. J )
         -> U. |^| T = U. J ) $=
      ( va vb ctop wss wcel cv cuni wceq wral w3a intss1 3ad2ant2 syl wa wb cvv
      cint uniss wrex wi ssel eqid topopn syl6 adantr imp eleq1 eqcoms ralimdva
      syl5ibrcom 3impia uniexg elintg mpbird elssuni sseq2 rcla4ev syl2an eqssd
      r19.21aiva uniss2 ) BFGZCBHZAIZJZCJZKZABLZMZBTZJZVIVLVMCGZVNVIGVFVEVOVKCB
      NOVMCUAPVLDIZEIZGZEVMUBZDCLVIVNGVLVSDCVLVIVMHZVPVIGZVSVPCHVLVTVIVGHZABLZV
      EVFVKWCVEVFQZVJWBABWDVGBHZQWBVJVHVGHZWDWEWFVEWEWFUCVFVEWEVGFHWFBFVGUDVGVH
      VHUEUFUGUHUIWBWFRVIVHVIVHVGUJUKUMULUNVLVISHZVTWCRVFVEWGVKCBUOOAVIBSUPPUQV
      PCURVRWAEVIVMVQVIVPUSUTVAVCDECVMVDPVB $.
      $( [27-Nov-2011] $)
  $}

  ${
    $d A a $.  $d J a $.
    $( The intersection of a non-empty finite family of open sets is open.
       (Contributed by FL, 20-Apr-2012.) $)
    neffopo $p |- ( J e. Top -> ( ( A C_ J /\ A =/= (/) /\ A e. Fin )
        -> |^| A e. J ) ) $=
      ( va wss c0 wne cfn wcel w3a ctop cint wi elpwg cv wceq sseq1 neeq1 com12
      cpw wal eleq1 3anbi123d inteq eleq1d imbi2d imbi12d wa istop2g ibi adantl
      cuni ax4 syl vtoclg 3exp com3r com4r syl6bir pm2.43a com4l pm2.43i 3imp )
      ABDZAEFZAGHZIZBJHZAKZBHZVCVDVEVGVILZVCVDVEVJLLVEVCVCVDVJVCVEVCVDVJLLZVEVC
      ABSZHZVEVKLABGMVEVCVDVMVJVCVDVEVMVJLZVCVDVEVNVMVFVJCNZBDZVOEFZVOGHZIZVGVO
      KZBHZLZLVFVJLCAVLVOAOZVSVFWBVJWCVPVCVQVDVRVEVOABPVOAEQVOAGUAUBWCWAVIVGWCV
      TVHBVOAUCUDUEUFVGVSWAVGVPVOUKBHLCTZVSWALZCTZUGZWEVGWGCJBUHUIWFWEWDWECULUJ
      UMRUNRUOUPUQURUSUTVAVBR $.
      $( [20-Apr-2012] $)
  $}

  ${
    $( If a subbase is included into a topology, so is the generated topology.
       (Contributed by FL, 20-Apr-2012.) $)
    sbincgt $p |- ( ( J e. Top /\ A C_ J ) -> ( topGen ` ( fi ` A ) ) C_ J ) $=
      ( ctop wcel wss cfi cfv ctg wceq wi tgtop ctb w3a ex sseq2 imbi2d syl5ibr
      eqcoms mpcom imp wa fibas a1i topbas adantr fitop fiss 3jca tgss syl6 wb
      ) BCDZABEZAFGZHGZBEZBHGZBIZULUMUPJZBKULUSURUMUOUQEZJULUMUNLDZBLDZUNBEZMZU
      TULUMVDULUMUAZVAVBVCVAVEAUBUCULVBUMBUDUEULUMVCBFGZBIULUMVCJZBUFULVGJBVFUL
      VGBVFIZUMUNVFEZJULUMVIABCUGNVHVCVIUMBVFUNOPQRSTUHNUNBUIUJURUPUTUMUPUTUKBU
      QBUQUOORPQST $.
      $( [25-Nov-2013] $) $( [20-Apr-2012] $)
  $}

  ${
    $d A v $.  $d J v $.  $d X v $.
    apnei.1 $e |- X = U. J $.
    $( Any point has a neighborhood.  (Contributed by FL, 15-Oct-2012.) $)
    apnei $p |- ( ( J e. Top /\ A e. X ) -> E. v v e. ( ( nei ` J ) ` { A } ) )
        $=
      ( ctop wcel wa cv csn cnei cfv wex wss snssi tpnei syl5ibcom impcom eleq1
      wrex rcla4ev syl11anc df-rex anidm exbii bitri sylib ) CFGZBDGZHZAIZBJZCK
      LLZGZAUMTZUNAMZUJDUMGZUQUOUIUHUQUIULDNUHUQBDOULCDEPQRZURUNUQADUMUKDUMSUAU
      BUOUNUNHZAMUPUNAUMUCUSUNAUNUDUEUFUG $.
      $( [15-Oct-2012] $)
  $}

  ${
    $( A neighborhood of a point can't be empty.  (Contributed by FL,
       15-Oct-2012.) $)
    npmp $p |- ( ( J e. Top /\ A e. X /\ N e. ( ( nei ` J ) ` { A } ) )
     -> N =/= (/) ) $=
      ( ctop wcel csn cnei cfv w3a c0 wne wa wn simp1 snnzg 3ad2ant2 jca 0nnei
      wi nelne2 ex 3ad2ant3 com12 3syl pm2.43i ) BEFZADFZCAGZBHIIZFZJZCKLZULUGU
      IKLZMKUJFNZULUMTULUGUNUGUHUKOUHUGUNUKADPQRUIBSULUOUMUKUGUOUMTUHUKUOUMCKUJ
      UAUBUCUDUEUF $.
      $( [15-Oct-2012] $)

  $}

  ${
    $d x y z w v u f g t e $.
    $( A basis for the standard topology over the extended reals.  (Contributed
       by FL, 14-Sep-2013.) $)
    basexre $p |- ( ran (,) u. { RR* } ) e. TopBases $=
      ( vz vw vx vy vt ve vf cioo cxr wcel cv cin wss wa wrex wi co wceq sylbi
      cr vg vv vu crn csn cun ctb wel wral wo elun cle wbr ifcl ad2ant2r adantr
      cif ancoms ad2ant2l iooin ineq12 sylan9req opreq1 eqeq1d opreq2 syl111anc
      rcla42ev exp32 exp31 com4t imp31 r19.23adv r19.23adva r19.23aiv imp ancom
      cxp cpw wf wfn wb ioof oprvelrn anbi12d mp2b bitri 3imtr4i orcd elsn cuni
      unirnioo elssuni ressxr sseq2 biimpd sstr df-ss eleq1 biimprd expcom ee02
      ffn syl mpsyl pm2.43i ineq2 eleq1d syl5ibr incom eqtr ineq1 impcom visset
      mpan elsnc inidm syl6eq inex1 sylibr olcd ccase ex com12 ssid eleq2 sseq1
      jctr rcla4ev syl2an 3impa rgen3 cvv iooex rnex unex isbasis2g ax-mp mpbir
      snexALT ) HUDZIUEZUFZUGJZABUHZBKZCKZDKZLZMZNZBUUBOZAUUHUIDUUBUICUUBUIZUUK
      CDAUUBUUBUUHUUFUUBJZUUGUUBJZAKZUUHJZUUKUUMUUNNUUHUUBJZUUPUUHUUHMZNZUUKUUP
      UUMUUNUUQUUMUUFYTJZUUFUUAJZUJZUUNUUQPUUFYTUUAUKUUNUVBUUQUUNUUGYTJZUUGUUAJ
      ZUJZUVBUUQPUUGYTUUAUKUVEUVBUUQUVEUVBNUUHYTJZUUHUUAJZUJZUUQUVCUUTUVDUVAUVH
      UVCUUTNZUVFUVGEKZFKZHQZUUFRZFIOZEIOZGKZUAKZHQZUUGRZUAIOZGIOZNZUBKZUCKZHQZ
      UUHRZUCIOUBIOZUVIUVFUVOUWAUWGUVNUWAUWGPZEIUVJIJZUVMUWHFIUWIUVKIJZUVMUWHUW
      IUWJNZUVMNZUVTUWGGIUWLUVPIJZNUVSUWGUAIUWKUVMUWMUVQIJZUVSUWGPZPUWMUWNUWKUV
      MUWOUWMUWNUWKUVMUWOPUWMUWNNZUWKNZUVMUVSUWGUWQUVMUVSNZNUVJUVPULUMZUVPUVJUQ
      ZIJZUVKUVQULUMZUVKUVQUQZIJZUWTUXCHQZUUHRZUWGUWQUXAUWRUWMUWIUXAUWNUWJUWSUV
      PUVJIUNUOUPUWQUXDUWRUWNUWJUXDUWMUWIUWJUWNUXDUXBUVKUVQIUNURUSUPUWQUWRUXEUV
      LUVRLZUUHUWKUWPUXGUXERUVJUVKUVPUVQUTURUVLUUFUVRUUGVAVBUWFUXFUWTUWDHQZUUHR
      UBUCUWTUXCIIUWCUWTRUWEUXHUUHUWCUWTUWDHVCVDUWDUXCRUXHUXEUUHUWDUXCUWTHVEVDV
      GVFVHVIVJVKVLVMVIVLVNVOUVIUUTUVCNZUWBUVCUUTVPIIVQZTVRZHVSZHUXJVTZUXIUWBWA
      WBUXJUXKHXBZUXMUUTUVOUVCUWAEFIIUUFHWCGUAIIUUGHWCWDWEWFUXLUXMUVFUWGWAWBUXN
      UBUCIIUUHHWCWEWGWHUVDUUTNUVFUVGUVDUUTUVFUVDUUGIRZUUTUVFPDIWIUUTUVFUXOUUFI
      LZYTJZUUTUXQYTWJZTRZUUTUUFUXRMZUUTUXQPZWKUUFYTWLTIMZUXSUXTUUFTMZUYAWMUXSU
      XTUYCUXRTUUFWNWOUYCUYBUYAUYCUYBNUUFIMZUYAUUFTIWPUYDUXPUUFRZUYAUUFIWQUYEUX
      QUUTUXPUUFYTWRWSSXCWTXAXDXEUXOUUHUXPYTUUGIUUFXFZXGXHSVOWHUVCUVANUVFUVGUVA
      UVCUVFUVAUUFIRZUVCUVFPCIWIUVCUVFUYGIUUGLZYTJZUVCUYIUXSUVCUUGUXRMZUVCUYIPZ
      WKUUGYTWLUYBUXSUYJUUGTMZUYKWMUXSUYJUYLUXRTUUGWNWOUYLUYBUYKUYLUYBNUUGIMZUY
      KUUGTIWPUYMUUGILZUUGRZUYKUUGIWQUYOUYIUVCUYHUYNRZUYOUYIUVCWAIUUGXIUYPUYONU
      YHUUGYTUYHUYNUUGXJXGXNWSSXCWTXAXDXEUYGUUHUYHYTUUFIUUGXKXGXHSXLWHUVDUVANZU
      VGUVFUYQUUHIRZUVGUVDUVAUYRUVDUXOUVAUYRPUUGIDXMXOUVAUYRUXOUXPIRZUVAUYGUYSU
      UFICXMZXOUYGUXPIILIUUFIIXKIXPXQSUXOUUHUXPIUYFVDXHSVOUUHIUUFUUGUYTXRXOXSXT
      YAUUHYTUUAUKXSYBSYCSVOUUPUURUUHYDYGUUJUUSBUUHUUBUUEUUHRUUDUUPUUIUURUUEUUH
      UUOYEUUEUUHUUHYFWDYHYIYJYKUUBYLJUUCUULWAYTUUAHYMYNIYSYOCDABUUBYLYPYQYR $.
      $( [14-Sep-2013] $)
  $}

  $( The standard topology over ` RR* ` .  (Contributed by FL, 15-Sep-2013.) $)
  stovr $p |- ( topGen ` ( ran (,) u. { RR* } ) ) e. Top $=
    ( cioo crn cxr csn cun ctb wcel ctg cfv ctop basexre tgcl ax-mp ) ABCDEZFGN
    HIJGKNLM $.
    $( [15-Sep-2013] $)

  ${
    cldifemp.1 $e |- X = U. J $.
    $( The closure of a class ` S ` is empty iff ` S ` is empty.  (Contributed
       by FL, 15-Sep-2013.) $)
    cldifemp $p |- ( ( J e. Top /\ S C_ X ) ->
      ( ( ( cls ` J ) ` S ) = (/) <-> S = (/) ) ) $=
      ( ctop wcel wss wa ccl cfv c0 wceq wi sscls sseq0 syl cls0 fveq2 eqeq1d
      ex syl5ibrcom adantr impbid ) BEFZACGZHZABIJZJZKLZAKLZUFAUHGZUIUJMABCDNUK
      UIUJAUHOTPUDUJUIMUEUDUIUJKUGJZKLBQUJUHULKAKUGRSUAUBUC $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d P n o $.  $d S n o $.  $d n o J $.  $d n o X $.
    neindisj2.1 $e |- X = U. J $.
    $( A point ` P ` belongs to the closure of a set ` S ` iff every
       neighborhood of ` P ` meets ` S ` .  (Contributed by FL,
       15-Sep-2013.) $)
    neindisj2 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
        ( P e. ( ( cls ` J ) ` S ) <->
          A. n e. ( ( nei ` J ) ` { P } ) ( n i^i S ) =/= (/) ) ) $=
      ( vo wcel wss w3a cfv cv cin c0 wne wi wral wa wrex ex ctop ccl csn elcls
      cnei isneip r19.29r pm3.35 ssrin sseq2 syl6bi syl5com necon3d com23 imp31
      wceq ss0 r19.23aivw syl adantl 3adant2 imp r19.21aiv opnneip ineq1 neeq1d
      weq rcla4cva idd 3exp com14 com3l mpcom 3expia com25 3imp1 impbida bitrd
      ) DUAHZBEIZAEHZJZABDUBKKHAGLZHZWCBMZNOZPZGDQZCLZBMZNOZCAUCDUEKKZQZGABDEFU
      DWBWHWMWBWHRWKCWLWBWHWIWLHZWKPWBWNWHWKVSWAWNWHWKPZPVTVSWARWNWIEIZWDWCWIIZ
      RZGDSZRWOAGDWIEFUFWSWOWPWSWHWKWSWHRWRWGRZGDSWKWRWGGDUGWTWKGDWDWQWGWKWDWGW
      QWKWDWGWQWKPWDWGRWFWQWKWDWFUHWQWJNWENWQWEWJIZWJNUPZWENUPZWCWIBUIXBXAWENIX
      CWJNWEUJWEUQUKULUMULTUNUOURUSTUTUKVAUNVBVCWBWMRWGGDVSVTWAWMWCDHZWGPVSXDWA
      WMVTWGVSXDWAWMVTWGPPPVSXDRWDWMVTWAWFVSXDWDWMVTWAWFPPZPZWCWLHZVSXDWDJZXFAD
      WCVDWMXGXHXEWMXGXHXEPZWMXGRWFXIWKWFCWCWLCGVGWJWENWIWCBVEVFVHWAXHVTWFWFWAX
      HVTWFWFPWAXHVTJWFVIVJVKUSTVLVMVNVOTVOVPVCVQVR $.
      $( [15-Sep-2013] $)
  $}


$( End of Topology $)

