$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Subcategories
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c SubCat $.
  $( Extend class notation with a function returning all the subcategories of a
     given category. $)
  csubc $a class SubCat $.

  ${
    $d x y a $.
    $( ` ( SubCat `` x ) ` is the set of all the subcategories of the category
       ` x ` .  All the objects and all the morphisms of the subcategory belong
       to the supercategory.  The identity of an object, the domain and the
       codomain of a morphism are the same in the subcategory and the
       supercategory.  The composition of the subcategory is a restriction of
       the composition of the supercategory. $)
    df-subc $a |- SubCat = { <. x , y >. | ( x e. Cat /\ y = { a e. Cat | (
    ( id_ ` a ) C_ ( id_ ` x ) /\ ( ( dom_ ` a ) C_ ( dom_ ` x )
    /\ ( cod_ ` a ) C_ ( cod_ ` x ) ) /\ ( o_ ` a ) C_ ( o_ ` x ) ) } ) } $.
  $}

  ${
    $d C a u $.  $d C a x y $.  $d D a u $.  $d D a x y $.  $d J a u $.
    $d J a x y $.  $d R a u $.  $d R a x y $.  $d T a x y $.
    issubcat.2 $e |- D = ( dom_ ` T ) $.
    issubcat.3 $e |- C = ( cod_ ` T ) $.
    issubcat.4 $e |- R = ( o_ ` T ) $.
    issubcat.5 $e |- J = ( id_ ` T ) $.
    $( The set of all the subcategories of ` T ` . $)
    issubcat $p |- ( T e. Cat -> ( SubCat ` T ) = { a e. Cat | (
    ( id_ ` a ) C_ J /\ ( ( dom_ ` a ) C_ D
    /\ ( cod_ ` a ) C_ C ) /\ ( o_ ` a ) C_ R ) } ) $=
      ( vx vy vu cv cid_ cfv wss cdom_ ccod_ wa co_ w3a ccat crab csubc wceq
      fveq2 syl6eqr sseq2d anbi12d 3anbi123d rabbidv df-subc cpw cxp cvv fvex
      eqeltri pwex xpex wcel c1st c2nd cop wrel relcat 1st2nd mpan adantr cdm
      reldcat 1stdm sylancr wi elpw ccom df-doma fveq1i wfun wfo fo1st fofun
      ax-mp visset wf fof fdmi eleqtrri fvco mp3an eqtri eleq1i ax-1 sylbi
      sylbir a1d a1i 3imp impcom df-coda fo2nd adantl 3ad2ant2 jca crn relrcat
      2ndrn df-ida simp1 df-cmpa 3exp elxp6 sylanbrc sseq1d elrab anbi1i
      anbi2i bitri 3imtr4i ssriv ssexi fvopab4 ) KLDFNZOPZKNZOPZQZYCRPZYERPZQZY
      CSPZYESPZQZTZYCUAPZYEUAPZQZUBZFUCUDYDEQZYHBQZYKAQZTZYOCQZUBZFUCUDZUCUEYED
      UFZYRUUDFUCUUFYGYSYNUUBYQUUCUUFYFEYDUUFYFDOPZEYEDOUGJUHUIUUFYJYTYMUUAUUFY
      IBYHUUFYIDRPZBYEDRUGGUHUIUUFYLAYKUUFYLDSPZAYEDSUGHUHUIUJUUFYPCYOUUFYPDUAP
      ZCYEDUAUGIUHUIUKULKLFUMUUEBUNZAUNZUOZEUNZCUNZUOZUOZUUMUUPUUKUULBBUUHUPGDR
      UQURUSAAUUIUPHDSUQURUSUTUUNUUOEEUUGUPJDOUQURUSCCUUJUPIDUAUQURUSUTUTMUUEUU
      QMNZUCVAZUUROPZEQZUURRPZBQZUURSPZAQZTZUURUAPZCQZUBZTZUURUURVBPZUURVCPZVDU
      FZUVKUVKVBPZUVKVCPZVDUFZUVNUUKVAZUVOUULVAZTZTZUVLUUPVAZTZTZUURUUEVAUURUUQ
      VAZUVJUVMUWBUUSUVMUVIUCVEZUUSUVMVFUURUCVGVHVIUVJUVTUWAUVJUVPUVSUUSUVPUVIU
      USUCVJZVEUVKUWFVAZUVPVKUWEUUSUWGVFUURUCVLVHUVKUWFVGVMVIUVJUVQUVRUVIUUSUVQ
      UVAUVFUVHUUSUVQVNZUVFUVHUWHVNZVNUVAUVCUWIUVEUVCUWHUVHUVCUVBUUKVAZUWHUVBBU
      URRUQVOUWJUVQUWHUVBUVNUUKUVBUURVBVBVPZPZUVNUURRUWKVQVRVBVSZUWMUURVBVJZVAZ
      UWLUVNUFUPUPVBVTZUWMWAUPUPVBWBWCZUWQUURUPUWNMWDZUPUPVBUWPUPUPVBWEWAUPUPVB
      WFWCWGWHZUURVBVBWIWJWKWLUVQUUSWMWNWOWPVIWQWRWSUVIUUSUVRUVFUVAUUSUVRVNZUVH
      UVEUWTUVCUVEUVDUULVAZUWTUVDAUURSUQVOUXAUVRUWTUVDUVOUULUVDUURVCVBVPZPZUVOU
      URSUXBWTVRVCVSZUWMUWOUXCUVOUFUPUPVCVTZUXDXAUPUPVCWBWCZUWQUWSUURVCVBWIWJWK
      WLUVRUUSWMWNWOXBXCWSXDXDUVJUVLUVLVBPZUVLVCPZVDUFZUXGUUNVAZUXHUUOVAZTZUWAU
      USUXIUVIUUSUCXEZVEUVLUXMVAZUXIXFUWEUUSUXNVFUURUCXGVHUVLUXMVGVMVIUVIUUSUXL
      UVAUVFUVHUUSUXLVNZUVAUVHUXOVNZUVFUVAUUTUUNVAZUXPUUTEUUROUQVOUXQUXJUXPUUTU
      XGUUNUUTUURVBVCVPZPZUXGUUROUXRXHVRUWMUXDUURVCVJZVAZUXSUXGUFUWQUXFUURUPUXT
      UWRUPUPVCUXEUPUPVCWEXAUPUPVCWFWCWGWHZUURVBVCWIWJWKWLUXJUVHUUSUXLUXJUVHUUS
      UBUXJUXKUXJUVHUUSXIUXJUVHUUSUXKUVHUUSUXKVNZVNUXJUVHUVGUUOVAZUYCUVGCUURUAU
      QVOUYDUXKUYCUVGUXHUUOUVGUURVCVCVPZPZUXHUURUAUYEXJVRUXDUXDUYAUYFUXHUFUXFUX
      FUYBUURVCVCWIWJWKWLUXKUUSWMWNWOWQWRXDXKWNWOWPWRWSUVLUUNUUOXLXMXDXDUUDUVIF
      UURUCYCUURUFZYSUVAUUBUVFUUCUVHUYGYDUUTEYCUUROUGXNUYGYTUVCUUAUVEUYGYHUVBBY
      CUURRUGXNUYGYKUVDAYCUURSUGXNUJUYGYOUVGCYCUURUAUGXNUKXOUWDUVMUVKUUMVAZUWAT
      ZTUWCUURUUMUUPXLUYIUWBUVMUYHUVTUWAUVKUUKUULXLXPXQXRXSXTYAYB $.
      $( [6-Oct-2009] $) $( [17-Sep-2009] $)
  $}

  ${
    $d C a $.  $d D a $.  $d J a $.  $d R a $.  $d T a $.  $d U a $.
    issubcata.2 $e |- D = ( dom_ ` T ) $.
    issubcata.3 $e |- C = ( cod_ ` T ) $.
    issubcata.4 $e |- R = ( o_ ` T ) $.
    issubcata.5 $e |- J = ( id_ ` T ) $.
    $( The predicate "is a subcategory of" ` T ` . $)
    issubcata $p |- ( T e. Cat -> ( U e. ( SubCat ` T ) <-> (
    U e. Cat /\ ( ( id_ ` U ) C_ J /\ ( ( dom_ ` U ) C_ D
    /\ ( cod_ ` U ) C_ C ) /\ ( o_ ` U ) C_ R ) ) ) ) $=
      ( va ccat wcel csubc cfv cv cid_ wss cdom_ ccod_ wa co_ w3a crab
      issubcat eleq2d wb wceq fveq2 sseq1d anbi12d 3anbi123d elrab a1i bitrd )
      DLMZEDNOZMEKPZQOZFRZURSOZBRZURTOZARZUAZURUBOZCRZUCZKLUDZMZELMEQOZFRZESOZB
      RZETOZARZUAZEUBOZCRZUCZUAZUPUQVIEABCDFKGHIJUEUFVJWAUGUPVHVTKELUREUHZUTVLV
      EVQVGVSWBUSVKFUREQUIUJWBVBVNVDVPWBVAVMBURESUIUJWBVCVOAURETUIUJUKWBVFVRCUR
      EUBUIUJULUMUNUO $.
      $( [6-Oct-2009] $) $( [17-Sep-2009] $)
  $}

  ${
    $d C a $.  $d D a $.  $d J a $.  $d R a $.  $d T a $.  $d U a $.
    issubcatb.2 $e |- D = ( dom_ ` T ) $.
    issubcatb.3 $e |- C = ( cod_ ` T ) $.
    issubcatb.4 $e |- R = ( o_ ` T ) $.
    issubcatb.5 $e |- J = ( id_ ` T ) $.
  $( Todo: to be removed. $)
    $( The predicate "is a subcategory of" ` T ` . $)
    issubcatb $p |- ( ( T e. Cat /\ U e. Cat ) -> ( U e. ( SubCat ` T ) <-> (
    ( id_ ` U ) C_ J /\ ( ( dom_ ` U ) C_ D
    /\ ( cod_ ` U ) C_ C ) /\ ( o_ ` U ) C_ R ) ) ) $=
      ( va ccat wcel csubc cfv cv cid_ wss cdom_ ccod_ wa co_ w3a crab
      issubcat eleq2d wceq fveq2 sseq1d anbi12d 3anbi123d elrab3 sylan9bb ) DLM
      ZEDNOZMEKPZQOZFRZUPSOZBRZUPTOZARZUAZUPUBOZCRZUCZKLUDZMELMEQOZFRZESOZBRZET
      OZARZUAZEUBOZCRZUCZUNUOVGEABCDFKGHIJUEUFVFVQKELUPEUGZURVIVCVNVEVPVRUQVHFU
      PEQUHUIVRUTVKVBVMVRUSVJBUPESUHUIVRVAVLAUPETUHUIUJVRVDVOCUPEUBUHUIUKULUM
      $.
      $( [6-Oct-2009] $) $( [17-Sep-2009] $)
  $}

  ${
    $d a x y $.
    $( Lemma to simplify some subcategories related theorems . $)
    besubbeca $p |- ( U e. ( SubCat ` T ) -> T e. Cat ) $=
      ( vx vy va csubc cfv wcel cdm ccat elfvdm cv cid_ wss cdom_ ccod_ wa co_
      w3a crab wceq copab df-subc dmeqi eleq2i dmopabss sseli sylbi syl ) BAFGH
      AFIZHZAJHZBAFKUKACLZJHDLELZMGUMMGNUNOGUMOGNUNPGUMPGNQUNRGUMRGNSEJTUAZQCDU
      BZIZHULUJUQAFUPCDEUCUDUEUQJAUOCDJUFUGUHUI $.
      $( [6-Oct-2009] $) $( [17-Sep-2009] $)
  $}

  $( A category belongs to the set of its subcategories. $)
  catsbc $p |- ( T e. Cat -> T e. ( SubCat ` T ) ) $=
    ( ccat wcel csubc cfv cid_ wss cdom_ ccod_ wa co_ w3a ssid pm3.2i 3pm3.2i
    wb eqid issubcatb anidms mpbiri ) ABCZAADECZAFEZUCGZAHEZUEGZAIEZUGGZJZAKEZU
    JGZLZUDUIUKUCMUFUHUEMUGMNUJMOUAUBULPUGUEUJAAUCUEQUGQUJQUCQRST $.
    $( [6-Oct-2009] $) $( [17-Sep-2009] $)

  ${
    obsubc2.1 $e |- O1 = dom ( id_ ` T ) $.
    obsubc2.2 $e |- O2 = dom ( id_ ` U ) $.
    $( The objects of a subcategory are a subset of those of the
       supercategory.  JFM CAT_2 th. 11 . $)
    obsubc2 $p |- ( U e. ( SubCat ` T ) -> O2 C_ O1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa cid_ cdm cdom_ ccod_ co_ w3a eqid
      issubcata dmss 3ad2ant1 adantl syl6bi imp 3sstr4g mpancom ) AGHZBAIJHZDCK
      ABLUIUJMBNJZOZANJZOZDCUIUJULUNKZUIUJBGHZUKUMKZBPJAPJZKBQJAQJZKMZBRJARJZKZ
      SZMUOUSURVAABUMURTUSTVATUMTUAVCUOUPUQUTUOVBUKUMUBUCUDUEUFFEUGUH $.
      $( [6-Oct-2009] $) $( [17-Sep-2009] $)
  $}

  ${
    idsubc.1 $e |- I1 = ( id_ ` T ) $.
    idsubc.2 $e |- I2 = ( id_ ` U ) $.
    $( The identity function of a subcategory is a subset of the identity
       function of the supercategory. $)
    idsubc $p |- ( U e. ( SubCat ` T ) -> I2 C_ I1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa cid_ cdom_ ccod_ co_ w3a eqid
      issubcata sseq2i biimpi 3ad2ant1 adantl syl6bi imp 3sstr4g mpancom ) AGHZ
      BAIJHZDCKABLUIUJMBNJZANJZDCUIUJUKULKZUIUJBGHZUKCKZBOJAOJZKBPJAPJZKMZBQJAQ
      JZKZRZMUMUQUPUSABCUPSUQSUSSETVAUMUNUOURUMUTUOUMCULUKEUAUBUCUDUEUFFEUGUH
      $.
      $( [6-Oct-2009] $) $( [17-Sep-2009] $)
  $}

  ${
    domsubc.1 $e |- D1 = ( dom_ ` T ) $.
    domsubc.2 $e |- D2 = ( dom_ ` U ) $.
    $( The domain function of a subcategory is a subset of the domain function
       of the supercategory. $)
    domsubc $p |- ( U e. ( SubCat ` T ) -> D2 C_ D1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa cdom_ cid_ ccod_ co_ w3a eqid
      issubcata simpr2l syl6bi imp 3sstr4g mpancom ) AGHZBAIJHZDCKABLUFUGMBNJZA
      NJZDCUFUGUHUIKZUFUGBGHZBOJAOJZKZUJBPJAPJZKZMBQJAQJZKZRMUJUNUIUPABULUISUNS
      UPSULSTUJUOUMUQUKUAUBUCFEUDUE $.
      $( [6-Oct-2009] $) $( [19-Sep-2009] $)
  $}

  ${
    codsubc.1 $e |- C1 = ( cod_ ` T ) $.
    codsubc.2 $e |- C2 = ( cod_ ` U ) $.
    $( The codomain function of a subcategory is a subset of the codomain
       function of the supercategory. $)
    codsubc $p |- ( U e. ( SubCat ` T ) -> C2 C_ C1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa ccod_ cid_ cdom_ co_ w3a eqid
      issubcata simpr2r syl6bi imp 3sstr4g mpancom ) AGHZBAIJHZDCKABLUFUGMBNJZA
      NJZDCUFUGUHUIKZUFUGBGHZBOJAOJZKZBPJAPJZKZUJMBQJAQJZKZRMUJUIUNUPABULUNSUIS
      UPSULSTUOUJUMUQUKUAUBUCFEUDUE $.
      $( [6-Oct-2009] $) $( [19-Sep-2009] $)
  $}


  $( A subcategory is a category. $)
  subctct $p |- ( U e. ( SubCat ` T ) -> U e. Cat ) $=
    ( ccat wcel csubc cfv besubbeca cid_ wss cdom_ ccod_ wa co_ w3a eqid
    issubcata simpl syl6bi mpcom ) ACDZBAEFDZBCDZABGTUAUBBHFAHFZIBJFAJFZIBKFAKF
    ZILBMFAMFZINZLUBUEUDUFABUCUDOUEOUFOUCOPUBUGQRS $.
    $( [6-Oct-2009] $) $( [17-Sep-2009] $)

  ${
    morsubc.1 $e |- M1 = dom ( dom_ ` T ) $.
    morsubc.2 $e |- M2 = dom ( dom_ ` U ) $.
    $( The morphisms of a subcategory are a subset of those of the
       supercategory. $)
    morsubc $p |- ( U e. ( SubCat ` T ) -> M2 C_ M1 ) $=
      ( csubc cfv wcel wss ccat cid_ cdom_ ccod_ wa co_ w3a wb besubbeca eqid
      issubcata syl cdm dmss 3sstr4g adantr 3ad2ant2 adantl syl6bi pm2.43i ) BA
      GHIZDCJZUKUKBKIZBLHALHZJZBMHZAMHZJZBNHANHZJZOZBPHAPHZJZQZOZULUKAKIUKVERAB
      SUSUQVBABUNUQTUSTVBTUNTUAUBVDULUMVAUOULVCURULUTURUPUCUQUCDCUPUQUDFEUEUFUG
      UHUIUJ $.
      $( [6-Oct-2009] $) $( [18-Sep-2009] $)
  $}

  ${
    cmpsubc.1 $e |- Ro1 = ( o_ ` T ) $.
    cmpsubc.2 $e |- Ro2 = ( o_ ` U ) $.
    $( The composition law of a subcategory is a subset of the composition law
       of the supercategory. $)
    cmpsubc $p |- ( U e. ( SubCat ` T ) -> Ro2 C_ Ro1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa co_ cid_ cdom_ ccod_ w3a eqid
      issubcata simpr3 syl6bi imp 3sstr4g mpancom ) AGHZBAIJHZDCKABLUFUGMBNJZAN
      JZDCUFUGUHUIKZUFUGBGHZBOJAOJZKZBPJAPJZKBQJAQJZKMZUJRMUJUOUNUIABULUNSUOSUI
      SULSTUKUMUPUJUAUBUCFEUDUE $.
      $( [6-Oct-2009] $) $( [20-Sep-2009] $)
  $}

  ${
    $d T x $.  $d U x $.
    idsubidsup.1 $e |- I1 = ( id_ ` T ) $.
    idsubidsup.2 $e |- I2 = ( id_ ` U ) $.
    idsubidsup.3 $e |- O2 = dom ( id_ ` U ) $.
    $( The identity of an an objet of the subcategory equals the identity of
       the object in the supercategory. $)
    idsubidsup $p |- ( U e. ( SubCat ` T ) ->
      A. x e. O2 ( I2 ` x ) = ( I1 ` x ) ) $=
      ( csubc cfv wcel cv wceq wa wfun wss cdm ccat calg besubbeca cded catded
      dedalg syl cdom_ wf eqid ida ffun 3syl adantr idsubc cid_ eqcomi dmeqi
      eqtri eleq2i biimpi adantl w3a funssfv eqcomd syl111anc r19.21aiva ) CBJK
      LZAMZFKZVGEKZNZADVFVGDLZOEPZFEQZVGFRZLZVJVFVLVKVFBSLZBTLZVLBCUAVPBUBLVQBU
      CBUDUEVQERZBUFKZRZEUGVLVSBEVTVRVTUHVSUHVRUHGUIVRVTEUJUEUKULVFVMVKBCEFGHUM
      ULVKVOVFVKVODVNVGDCUNKZRVNIWAFFWAHUOUPUQURUSUTVLVMVOVAVIVHVGEFVBVCVDVE $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

  ${
    $d M m n o p $.  $d T m n o p $.  $d U m n o p $.
    idsubfun.1 $e |- M = dom ( dom_ ` U ) $.
    $( The identity restricted to the morphism of a subcategory ` U ` is a
       functor from the subcategory to the supercategory.  It is called the
       inclusion functor.  JFM CAT_2 th. 19. $)
    idsubfun $p |- ( U e. ( SubCat ` T ) ->
      ( _I |` M ) e. ( Func ` <. U , T >. ) ) $=
      ( vo vp vm vn csubc cfv wcel ccat cid cres cop cfunc besubbeca subctct
      cvv wa wi cdom_ cdm fvex a1i dmexg syl syl5eqel resiexg w3a wf cv cid_
      wceq wrex wral ccod_ co_ co wss wf1o f1oi f1of ax-mp eqid morsubc adantl
      fss sylancr obsubc2 ssel fveq2 eqeq2d rcla4ev ex fvi jdmo a1dd 3ad2ant1
      3imp fvres wfun idc ffun 3ad2ant2 idsubc 3ad2ant3 simp2 funssfv
      syl111anc 3eqtr4a syl5com 3exp com14 syl6 com13 pm2.43i com4l mpcom
      impcom r19.21aiv simpll1 simpl1 eleq2i biimpi dmo syl2an syl11anc eqtrd
      ad2antrr ad2antlr domc domsubc eqcomd fveq2d eqtr3d eqtr2d 3eqtrd cdmo
      sylan codc codsubc morcat syl5eq adantr eleq2d biimpa jca id com23 imp31
      cmpmorp sylc oprex fvresi adantll opreq12 cmppfc1 cmpsubc opeq12d wb
      cmppfcd mpbird eqeltrd oprssoprvg eqtr4d exp31 3jca isfunb 3exp1 mpd
      mp2and ) BAIJKZALKZBLKZMCNZBAOPJKZABQABRUUMUUPSKZUUNUUOTZUUQUAUUMCSKUURUU
      MBUBJZUCZSCUUMUUTSKZUVASKUVBUUMBUBUDUEUUTSUFUGDUHCSUIUGUUSUURUUMUUQUUOUUN
      UURUUMUUQUAUAUUOUUNUURUUMUUQUUOUUNUURUJZUUMTZUUQCAUBJZUCZUUPUKZEULZBUMJZJ
      ZUUPJZFULZAUMJZJZUNZFUVMUCZUOZEUVIUCZUPZGULZUUTJZUVIJZUUPJZUVTUUPJZUVEJZU
      VMJZUNZGCUPZUVTBUQJZJZUVIJZUUPJZUWDAUQJZJZUVMJZUNZGCUPZTZHULZUWIJZUWAUNZU
      VTUWSBURJZUSZUUPJZUWDUWSUUPJZAURJZUSZUNZUAZHCUPZGCUPZUJZTZUVDUVGUXLUVDCCU
      UPUKZCUVFUTZUVGCCUUPVAUXNCVBCCUUPVCVDUUMUXOUVCABUVFCUVFVEZDVFVGCCUVFUUPVH
      VIUVDUVSUWRUXKUVDUVQEUVRUUMUVCUVHUVRKZUVQUAZUVRUVPUTZUUMUVCUXRUAABUVPUVRU
      VPVEZUVRVEZVJUXQUXSUUMUVCUVQUXQUXSUUMUVCUVQUAUAZUAUXSUXQUXQUYBUXSUXQUVHUV
      PKZUXQUYBUAUVRUVPUVHVKUVCUXQUUMUYCUVQUVCUXQUUMUYCUVQUAUYCUVKUVHUVMJZUNZUV
      QUVCUXQUUMUJZUYCUYEUVQUVOUYEFUVHUVPUVLUVHUNUVNUYDUVKUVLUVHUVMVLVMVNVOUYFU
      VJMJZUVJUVKUYDUVJSKUYGUVJUNUVHUVIUDUVJSVPVDUYFUVJCKZUVKUYGUNUVCUXQUUMUYHU
      UOUUNUXQUUMUYHUAUAUURUUOUXQUYHUUMUUOUXQUYHUVHBUVICUVRDUYAUVIVEZVQVOVRVSVT
      UVJCMWAUGUYFUVMWBZUVIUVMUTZUXQUYDUVJUNUVCUXQUYJUUMUUNUUOUYJUURUUNUVPUVFUV
      MUKUYJUVEAUVMUVFUVPUXPUVEVEZUXTUVMVEZWCUVPUVFUVMWDUGWEZVSUUMUVCUYKUXQABUV
      MUVIUYMUYIWFZWGUVCUXQUUMWHUVHUVMUVIWIWJWKWLWMWNWOWPWQWRWSWTXAUVDUWHUWQUVD
      UWGGCUVDUVTCKZUWGUVDUYPTZUWCUWBUVTUVEJZUVMJZUWFUYQUWCUWBMJZUWBUYQUWBCKZUW
      CUYTUNUYQUUOUWAUVRKZVUAUUOUUNUURUUMUYPXBZUVDUUOUVTUVAKZVUBUYPUUOUUNUURUUM
      XCZUYPVUDCUVAUVTDXDXEZUUTBUVTUVAUVRUVAVEUYAUUTVEZXFXGZUWABUVICUVRDUYAUYIV
      QXHUWBCMWAUGUYQUWBSKZUYTUWBUNVUIUYQUWAUVIUDUEUWBSVPUGXIUYQUWAUVMJZUWBUYSU
      YQUYJUYKVUBVUJUWBUNUVCUYJUUMUYPUYNXJZUUMUYKUVCUYPUYOXKZVUHUWAUVMUVIWIWJUY
      QUWAUYRUVMUYQUYRUWAUYQUVEWBZUUTUVEUTZVUDUYRUWAUNUVCVUMUUMUYPUUNUUOVUMUURU
      UNUVFUVPUVEUKVUMUVEAUVMUVFUVPUXPUYLUXTUYMXLUVFUVPUVEWDUGWEXJUUMVUNUVCUYPA
      BUVEUUTUYLVUGXMXKUYPVUDUVDVUFVGUVTUVEUUTWIWJXNXOXPUYQUYRUWEUVMUYQUVTUWDUV
      EUYQUWDUVTMJZUVTUYPUWDVUOUNUVDUVTCMWAVGUYPVUOUVTUNUVDUVTCVPVGXQZXOXOXRVOX
      AUVDUWPGCUVDUYPUWPUYQUWLUWKUVTUWMJZUVMJZUWOUYQUWLUWKMJZUWKUYQUWKCKZUWLVUS
      UNUYQUUOUWJUVRKZVUTVUCUVDUUOUYPVVAVUEUWIBUVTCUVRDUYAUWIVEZXSXTZUWJBUVICUV
      RDUYAUYIVQXHUWKCMWAUGUYQUWKSKZVUSUWKUNVVDUYQUWJUVIUDUEUWKSVPUGXIUYQUWKUWJ
      UVMJZVURUYQUYJUYKVVAUWKVVEUNVUKVULVVCUYJUYKVVAUJVVEUWKUWJUVMUVIWIXNWJUYQU
      WJVUQUVMUYQVUQUWJUYQUWMWBZUWIUWMUTZUVTUWIUCZKZVUQUWJUNUVCVVFUUMUYPUUNUUOV
      VFUURUUNUVFUVPUWMUKVVFUWMUVEAUVMUVFUVPUXPUYLUXTUYMUWMVEZYAUVFUVPUWMWDUGWE
      XJUUMVVGUVCUYPABUWMUWIVVJVVBYBXKUVDUYPVVIUVDCVVHUVTUVCCVVHUNZUUMUUOUUNVVK
      UURUUOUVAVVHCBYCDYDVSYEYFYGUVTUWMUWIWIWJXNXOXIUYQVUQUWNUVMUYQUVTUWDUWMVUP
      XOXOXRVOXAYHUVDUXJGCUVDUYPUXJUYQUXIHCUYQUWSCKZUXAUXHUYQVVLTZUXATZUXDUXCMJ
      ZUXCUXGVVNUXCCKZUXDVVOUNVVNUUOVVLUYPUJZUWAUWTUNZVVPVVMVVQUXAUVDUYPVVLVVQU
      VCUYPVVLVVQUAUAZUUMUUOUUNVVSUURUUOVVLUYPVVQUUOVVLUYPVVQVVQYIWMYJVSYEYKYEZ
      UXAVVRVVMUXAUWTUWAUXAYIXNVGZUWIUUTUXBBUWSUVTCDVUGVVBUXBVEZYLYMUXCCMWAUGVV
      OUXCUNZVVNUXCSKVWCUVTUWSUXBYNUXCSVPVDUEVVNUXCUWDUXEUXBUSZUXGVVNUVTUWDUNZU
      WSUXEUNZTZUXCVWDUNVVMVWGUXAUYPVVLVWGUVDUYPVVLTZVWEVWFVWHUWDUVTUYPUWDUVTUN
      ZVVLCUVTYOZYEXNVWHUXEUWSMJZUWSVVLUXEVWKUNUYPUWSCMWAVGVVLVWKUWSUNUYPUWSCVP
      VGXQYHYPYEUVTUWDUWSUXEUXBYQUGVVNUXFWBZUXBUXFUTZUWDUXEOZUXBUCZKUXGVWDUNUYQ
      VWLVVLUXAUVCVWLUUMUYPUUNUUOVWLUURUXFAUXFVEZYRWEXJXJUYQVWMVVLUXAUUMVWMUVCU
      YPABUXFUXBVWPVWBYSXKXJVVNVWNUVTUWSOZVWOVVNUWDUVTUXEUWSUYQVWIVVLUXAUYPVWIU
      VDVWJVGXJVVLUXEUWSUNUYQUXACUWSYOXKYTVVNVWQVWOKZVVRVWAVVNVVQVWRVVRUUAVVTUW
      IUUTUXBBUWSUVTCDVUGVVBVWBUUBUGUUCUUDUWDUXEUXFUXBUUEWJUUFXRUUGXAVOXAUUHYHU
      VCUUQUXMUUAUUMSBAGHEUUPFUVRCUVPUVFUVIUUTUWIUXBUVMUVEUWMUXFUYADVUGVVBUYIVW
      BUXTUXPUYLVVJUYMVWPUUIYEUUCUUJWTWPUUKUUL $.
      $( [6-Oct-2009] $) $( [5-Oct-2009] $)
  $}

  ${
    $d M1 x $.  $d M2 x $.
    infemb.1 $e |- M1 = dom ( dom_ ` T ) $.
    infemb.2 $e |- M2 = dom ( dom_ ` U ) $.
    $( The inclusion functor is an embedding. $)
    infemb $p |- ( U e. ( SubCat ` T ) -> ( _I |` M2 ) : M2 -1-1-> M1 ) $=
      ( vx csubc cfv wcel cv cid cres wf1o wss wa wex wf1 morsubc f1oi jctil
      cdom_ cdm cvv fvex dmex eqeltri wceq f1oeq3 sseq1 anbi12d cla4ev syl
      resiexg ax-mp f11o sylibr ) BAHIJZDGKZLDMZNZUSCOZPZGQZDCUTRURDDUTNZDCOZPZ
      VDURVFVEABCDEFSDTUAVCVGGDDBUBIZUCUDFVHBUBUEUFUGZUSDUHVAVEVBVFUSDDUTUIUSDC
      UJUKULUMGDCUTDUDJUTUDJVIDUDUNUOUPUQ $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

