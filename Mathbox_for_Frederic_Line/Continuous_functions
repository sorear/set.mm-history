$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Continuous functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x F $.  $d x J $.  $d x K $.  $d x L $.
    $( A mapping remains continuous when the topology associated to its domain
       is replaced by a finer one.  (Contributed by FL, 22-May-2008.) $)
    cnrsfin $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( F e. ( J Cn L ) /\ U. J = U. K /\ J C_ K ) -> F e. ( K Cn L ) ) ) $=
      ( vx ctop wcel w3a ccn co cuni wceq wss wf wral wa wi wb eqid iscn biimpd
      ccnv cv cima 3adant2 ad2antrr feq2 adantl ralimdv ad2antlr anim12d sylbid
      ssel exp31 com24 3impd 3adant1 sylibrd ) BFGZCFGZDFGZHZABDIJGZBKZCKZLZBCM
      ZHVEDKZANZAUBEUCUDZCGZEDOZPZACDIJGZVBVCVFVGVMVBVGVFVCVMVBVGVFVCVMQVBVGPZV
      FPZVCVDVHANZVJBGZEDOZPZVMVBVCVTRZVGVFUSVAWAUTEABDVDVHVDSVHSZTUEUFVPVQVIVS
      VLVFVQVIQVOVFVQVIVDVEVHAUGUAUHVGVSVLQVBVFVGVRVKEDBCVJUMUIUJUKULUNUOUPUTVA
      VNVMRUSEACDVEVHVESWBTUQUR $.
      $( [1-Jun-2008] $) $( [22-May-2008] $)
  $}

  ${
    $d x F $.  $d x J $.  $d x K $.  $d x L $.
    $( A mapping remains continuous when the topology associated to its range
       is replaced by a coarser one.  (Contributed by FL, 1-Jun-2008.) $)
    cnrscoa $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( F e. ( J Cn L ) /\ U. L = U. K /\ K C_ L ) ->
         F e. ( J Cn K ) ) ) $=
      ( vx ctop wcel w3a ccn co cuni wceq wss wf wral wa wi wb eqid iscn biimpd
      ccnv cv cima 3adant2 ad2antrr adantl ssralv ad2antlr anim12d sylbid exp31
      feq3 com24 3impd 3adant3 sylibrd ) BFGZCFGZDFGZHZABDIJGZDKZCKZLZCDMZHBKZV
      DANZAUBEUCUDBGZECOZPZABCIJGZVAVBVEVFVKVAVFVEVBVKVAVFVEVBVKQVAVFPZVEPZVBVG
      VCANZVIEDOZPZVKVAVBVQRZVFVEURUTVRUSEABDVGVCVGSZVCSTUEUFVNVOVHVPVJVEVOVHQV
      MVEVOVHVCVDVGAUMUAUGVFVPVJQVAVEVIECDUHUIUJUKULUNUOURUSVLVKRUTEABCVGVDVSVD
      STUPUQ $.
      $( [1-Jun-2008] $)
  $}


  ${
    $d A x $.  $d B x $.  $d F x $.  $d J x $.
    mapdiscn.1 $e |- F e. C $.
    mapdiscn.2 $e |- B = U. J $.
    mapdiscn.3 $e |- A e. _V $.
    $( mapdiscn.1 is useless. see fnex. $)
    $( Lemma for ~ mapdiscn .  Any mapping whose domain is associated to the
       discrete topology is continuous. $)
    mapdiscnlem $p |- ( ( J e. Top /\ F : A --> B ) ->
      F e. ( ~P A Cn J ) ) $=
      ( vx ctop wcel wf wa cuni eqcomi wss wi cvv wb eqid cpw co distop ccnv cv
      ccn cima wral unipw feq23i biimpi adantl crn imassrn cdm dfdm4 sseq2i fdm
      wceq sseq2 biimpd fex mpan2 cnvexg syl imaexg elpwg 3syl biimprd ad2antlr
      syl9 mpcom com12 sylbi ax-mp ex ralrimiv jca iscn adantr mpbird mpanl1 )
      AUAZJKZEJKZABDLZDWCEUFUBKZAHUCWDWEMZWFMZWGWCNZENZDLZDUDZIUEZUGZWCKZIEUHZM
      ZWIWLWQWFWLWHWFWLABWJWKDWJAAUIOGUJUKULWIWPIEWIWNEKZWPWOWMUMZPZWIWSMZWPQZW
      MWNUNXAWODUOZPZXCWTXDWOXDWTDUPOUQXBXEWPWFXEWPQZWHWSXDAUSZWFXFABDURXGXEWOA
      PZWFWPXGXEXHXDAWOUTVAWFWPXHWFWMRKZWORKWPXHSWFDRKZXIWFARKXJHABRDVBVCDRVDVE
      WMWNRVFWOARVGVHVIVKVLVJVMVNVOVPVQVRWHWGWRSWFIDWCEWJWKWJTWKTVSVTWAWB $.
      $( [1-Jun-2008] $) $( [22-May-2008] $)
  $}

  ${
    $d A a $.  $d B a f $.  $d F a f $.  $d J a f $.
    mapdiscn2.2 $e |- B = U. J $.
    $( Any mapping whose domain is associated to the discrete topology is
       continuous.  (Contributed by FL, 19-Sep-2011.) $)
    mapdiscn $p |- ( ( J e. Top /\ F : A --> B /\ A e. D ) ->
      F e. ( ~P A Cn J ) ) $=
      ( va vf wcel wf cpw ccn co cv wi wceq imbi12d imbi2d cvv vex ctop wfn ffn
      feq2 pweq oveq1d eleq2d wa fnex eleq1 mapdiscnlem expcom vtoclg syl mpan2
      feq1 mpcom com12 com3l 3imp ) EUAIZABDJZACIZDAKZELMZIZVCVAVBVFVAGNZBDJZDV
      GKZELMZIZOZOVAVBVFOZOGACVGAPZVLVMVAVNVHVBVKVFVGABDUDVNVJVEDVNVIVDELVGAUEU
      FUGQRVHVAVKDVGUBZVHVAVKOZVGBDUCVOVGSIZVHVPOZGTZVOVQUHDSIVRVGSDUIVGBHNZJZV
      AVTVJIZOZOVRHDSVTDPZWAVHWCVPVGBVTDUPWDWBVKVAVTDVJUJRQVAWAWBVGBSVTEHTFVSUK
      ULUMUNUOUQURUMUSUT $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.  $d J x $.
    mapudiscn.2 $e |- A = U. J $.
    mapudiscn.3 $e |- B e. _V $.
    $( Any mapping whose range is associated to the undiscrete topology is
       continuous.  (Contributed by FL, 1-Jun-2008.) $)
    mapudiscn $p |- ( ( J e. Top /\ F : A --> B ) ->
      F e. ( J Cn { (/) , B } ) ) $=
      ( vx ctop wcel c0 cpr wf wa cima wceq wi imaeq2 eleq1d syl5ibr cun ccn co
      indistop ccnv cv wral simpr wo vex elpr ima0 0opn syl5eqel adantrd topopn
      fimacnv eleq1 syl impcom jaoi com12 adantlr syl5bi ralrimiv wb cuni unipr
      jca 0ex uncom un0 3eqtrri iscn adantr mpbird mpanl2 ) DHIZJBKZHIZABCLZCDV
      RUAUBIZBUCVQVSMZVTMZWAVTCUDZGUEZNZDIZGVRUFZMZWCVTWHWBVTUGWCWGGVRWEVRIWEJO
      ZWEBOZUHZWCWGWEJBGUIUJVQVTWLWGPVSWLVQVTMZWGWJWMWGPWKWJVQWGVTVQWGWJWDJNZDI
      VQWNJDWDUKDULUMWJWFWNDWEJWDQRSUNWMWGWKWDBNZDIZVTVQWPVTWOAOZVQWPPABCUPVQWP
      WQADIDAEUOWOADUQSURUSWKWFWODWEBWDQRSUTVAVBVCVDVHWBWAWIVEVTGCDVRABEVRVFJBT
      BJTBJBVIFVGJBVJBVKVLVMVNVOVP $.
      $( [1-Jun-2008] $)
  $}

  ${
    $d J g s v $.  $d X g s v $.
    sallnei.1 $e |- X = U. J $.
    $( Two ways to state the set of all the neighborhoods.  (Contributed by FL,
       2-Aug-2009.) $)
    sallnei $p |- ( J e. Top
                      -> U. ran ( nei ` J )
                         = { v
                           | ( v C_ X /\ E. g e. J g C_ v ) } ) $=
      ( vs ctop wcel cfv cv ciun wss wa wrex cab wceq syl vex elpw c0 cnei cuni
      crn cpw wfn neif eqcomd wral crab neival df-rab anbi1i abbii eqtri syl6eq
      fniunfv ex syl5bi ralrimiv iuneq2 iunab wb reximi anim2i adantl rexlimiva
      simpr 0ss biantrur rexbii 0elpw sseq1 anbi1d rexbidv rcla4ev mpan sylan2b
      anbi2d impbii a1i abbidv syl5eq 3eqtrd ) CGHZCUAIZUCUBZFDUDZFJZWEIZKZFWGA
      JZDLZWHBJZLZWMWKLZMZBCNZMZAOZKZWLWOBCNZMZAOZWDWEWGUEZWFWJPCDEUFXDWJWFFWGW
      EUPUGQWDWIWSPZFWGUHWJWTPWDXEFWGWHWGHZWHDLZWDXEWHDFRSWDXGXEWDXGMWIWQAWGUIZ
      WSAWHBCDEUJXHWKWGHZWQMZAOWSWQAWGUKXJWRAXIWLWQWKDARSULUMUNUOUQURUSFWGWIWSU
      TQWDWTWRFWGNZAOXCWRFAWGVAWDXKXBAXKXBVBWDXKXBWRXBFWGWRXBXFWQXAWLWPWOBCWNWO
      VGVCVDVEVFXAWLTWMLZWOMZBCNZXKWOXMBCXLWOWMVHVIVJTWGHWLXNMZXKDVKWRXOFTWGWHT
      PZWQXNWLXPWPXMBCXPWNXLWOWHTWMVLVMVNVRVOVPVQVSVTWAWBWC $.
      $( [11-Nov-2013] $) $( [2-Aug-2009] $)
  $}

  ${
    $d J x y z $.  $d X y $.
    nsn.1 $e |- X = U. J $.
    $( The neighborhoods of the singletons are neighborhoods.  (Contributed by
       FL, 2-Aug-2009.) $)
    nsn $p |- ( J e. Top ->
      U_ x e. X ( ( nei ` J ) ` { x } ) C_ U. ran ( nei ` J ) ) $=
      ( vy vz ctop wcel cv csn cnei cfv cuni wrex wi cpw wfn neif wceq syl ciun
      crn cdm eliun snelpw fndm eleq2 biimpd eqcoms syl5bi fveq2 eleq2d rcla4ev
      vex ex syl6 com3l rexlimiv sylbi com12 wfun wb eqid fnfun elunirn sylibrd
      ssrdv ) BGHZEACAIZJZBKLZLZUAZVKUBMZVHEIZVMHZVOFIZVKLZHZFVKUCZNZVOVNHZVPVH
      WAVPVOVLHZACNVHWAOZAVOCVLUDWCWDACVHVICHZWCWAVHWEVJVTHZWCWAOWEVJCPZHZVHWFV
      ICAUNUEVHVKWGQZWHWFOZBCDRWIVTWGSWJWGVKUFWJWGVTWGVTSWHWFWGVTVJUGUHUITTUJWF
      WCWAVSWCFVJVTVQVJSVRVLVOVQVJVKUKULUMUOUPUQURUSUTVHVKVAZWBWAVBVHVKBMZPZQWK
      BWLWLVCRWMVKVDTFVOVKVETVFVG $.
      $( [2-Aug-2009] $)
  $}

  ${
    $d A a f $.  $d A f x y $.  $d J a f $.  $d J f x y $.  $d X f x y $.
    osneisi.1 $e |- X = U. J $.
    $( The non empty open sets are neighborhoods of the singletons.
       (Contributed by FL, 16-Jul-2009.) $)
    osneisi $p |- ( ( J e. Top /\ A =/= (/) ) -> ( A e. J ->
       A e. U_ x e. X ( ( nei ` J ) ` { x } ) ) ) $=
      ( vy vf va ctop wcel c0 wne wa cv cfv wrex wss wi wceq ex csn cvv wfn wex
      cnei ciun wral elex ac5g neeq1 fveq2 eleq12d imbi12d rcla4v pm2.27 elunii
      id cuni eqcomi eleq2i elssuni syl6sseqr ad2antlr snssi ssid jctir anbi12d
      sseq2 sseq1 rcla4ev adantr jca sneq sseq1d anbi1d rexbidva anbi2d syl5com
      mpan9 syl5bi mpid syl6 com4l pm2.43a adantl exlimiv 3syl pm2.43i imp31 wb
      simpll isnei syl2an mpbird eliun sylibr ) CIJZBKLZMZBCJZBADANZUAZCUEOOZUF
      JZWSWTMZBXCJZADPZXDXEXGBDQZXBFNZQZXIBQZMZFCPZMZADPZWQWRWTXOWQWRWTXORRZWQC
      UBJGNZCUCZHNZKLZXSXQOZXSJZRZHCUGZMZGUDWQXPRZCIUHHCGUIYEYFGYDYFXRWTYDWQWRX
      OYDWTWQWRXORRZWTYDWRBXQOZBJZRZWTYGRYCYJHBCXSBSZXTWRYBYIXSBKUJYKYAYHXSBXSB
      XQUKYKUQULUMUNWRYJWTWQXOWRYJYIWTWQXORZRWRYIUOYIWTYLYIWTMZWQYHCURZJZXOYHBC
      UPYMWQYOXORYOYHDJZYMWQMZXOYNDYHDYNEUSUTYQXHYHUAZXIQZXKMZFCPZMZYPXOYQXHUUA
      WTXHYIWQWTBYNDBCVAEVBVCYMUUAWQYIYRBQZBBQZMZWTUUAYIUUCUUDYHBVDBVEVFWTUUEUU
      AYTUUEFBCXIBSYSUUCXKUUDXIBYRVHXIBBVIVGVJTVSVKVLYPUUBXOXNUUBAYHDXAYHSZXMUU
      AXHUUFXLYTFCUUFXICJZMZXJYSXKUUHXBYRXIUUFXBYRSUUGXAYHVMVKVNVOVPVQVJTVRVTTW
      ATWBWCWBWDWCWEWFWGWHWIXEXFXNADXEWQXBDQXFXNWJXADJWQWRWTWKXADVDXBFCBDEWLWMV
      PWNABDXCWOWPT $.
      $( [16-Jul-2009] $)
  $}
  $( The discrete topology on a set ` A ` .  (Contributed by FL,
     19-Sep-2011.) $)
  distopg $p |- ( A e. V -> ~P A e. Top ) $=
    ( wcel cvv cpw ctop elex cif wceq pweq eleq1d 0ex elimel distop dedth syl
    c0 ) ABCADCZAEZFCZABGRTRAQHZEZFCAQAUAISUBFAUAJKUAAQDLMNOP $.
    $( [19-Sep-2011] $)

  ${
    elsubops.1 $e |- K = ( topGen ` ( fi ` S ) ) $.
    $( The elements of a subbase are open sets.  (Contributed by FL,
       16-Apr-2012.) $)
    elsubops $p |- ( S e. A -> S C_ K ) $=
      ( wcel cfi cfv ctg wss ssfii fibas bastg ax-mp sstr sylancl eqcomi sseq2i
      ctb sylib ) BAEZBBFGZHGZIZBCITBUAIUAUBIZUCBAJUAREUDBKUALMBUAUBNOUBCBCUBDP
      QS $.
      $( [14-Dec-2013] $) $( [16-Apr-2012] $)
  $}

