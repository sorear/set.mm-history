$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Continuous functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x F $.  $d x J $.  $d x K $.  $d x L $.
    $( A mapping remains continuous when the topology associated to its domain
       is replaced by a finer one.  (Contributed by FL, 22-May-2008.) $)
    cnrsfin $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( F e. ( J Cn L ) /\ U. J = U. K /\ J C_ K ) -> F e. ( K Cn L ) ) ) $=
      ( vx ctop wcel w3a ccn co cuni wceq wss wf wral wa wi wb eqid iscn biimpd
      ccnv cv cima 3adant2 ad2antrr feq2 adantl ralimdv ad2antlr anim12d sylbid
      ssel exp31 com24 3impd 3adant1 sylibrd ) BFGZCFGZDFGZHZABDIJGZBKZCKZLZBCM
      ZHVEDKZANZAUBEUCUDZCGZEDOZPZACDIJGZVBVCVFVGVMVBVGVFVCVMVBVGVFVCVMQVBVGPZV
      FPZVCVDVHANZVJBGZEDOZPZVMVBVCVTRZVGVFUSVAWAUTEABDVDVHVDSVHSZTUEUFVPVQVIVS
      VLVFVQVIQVOVFVQVIVDVEVHAUGUAUHVGVSVLQVBVFVGVRVKEDBCVJUMUIUJUKULUNUOUPUTVA
      VNVMRUSEACDVEVHVESWBTUQUR $.
      $( [1-Jun-2008] $) $( [22-May-2008] $)
  $}

  ${
    $d x F $.  $d x J $.  $d x K $.  $d x L $.
    $( A mapping remains continuous when the topology associated to its range
       is replaced by a coarser one.  (Contributed by FL, 1-Jun-2008.) $)
    cnrscoa $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( F e. ( J Cn L ) /\ U. L = U. K /\ K C_ L ) ->
         F e. ( J Cn K ) ) ) $=
      ( vx ctop wcel w3a ccn co cuni wceq wss wf wral wa wi wb eqid iscn biimpd
      ccnv cv cima 3adant2 ad2antrr adantl ssralv ad2antlr anim12d sylbid exp31
      feq3 com24 3impd 3adant3 sylibrd ) BFGZCFGZDFGZHZABDIJGZDKZCKZLZCDMZHBKZV
      DANZAUBEUCUDBGZECOZPZABCIJGZVAVBVEVFVKVAVFVEVBVKVAVFVEVBVKQVAVFPZVEPZVBVG
      VCANZVIEDOZPZVKVAVBVQRZVFVEURUTVRUSEABDVGVCVGSZVCSTUEUFVNVOVHVPVJVEVOVHQV
      MVEVOVHVCVDVGAUMUAUGVFVPVJQVAVEVIECDUHUIUJUKULUNUOURUSVLVKRUTEABCVGVDVSVD
      STUPUQ $.
      $( [1-Jun-2008] $)
  $}


  ${
    $d A x $.  $d B x $.  $d F x $.  $d J x $.
    mapdiscn.1 $e |- F e. C $.
    mapdiscn.2 $e |- B = U. J $.
    mapdiscn.3 $e |- A e. _V $.
    $( mapdiscn.1 is useless. see fnex. $)
    $( Lemma for ~ mapdiscn .  Any mapping whose domain is associated to the
       discrete topology is continuous. $)
    mapdiscnlem $p |- ( ( J e. Top /\ F : A --> B ) ->
      F e. ( ~P A Cn J ) ) $=
      ( vx ctop wcel wf wa cuni eqcomi wss wi cvv wb eqid cpw co distop ccnv cv
      ccn cima wral unipw feq23i biimpi adantl crn imassrn cdm dfdm4 sseq2i fdm
      wceq sseq2 biimpd fex mpan2 cnvexg syl imaexg elpwg 3syl biimprd ad2antlr
      syl9 mpcom com12 sylbi ax-mp ex ralrimiv jca iscn adantr mpbird mpanl1 )
      AUAZJKZEJKZABDLZDWCEUFUBKZAHUCWDWEMZWFMZWGWCNZENZDLZDUDZIUEZUGZWCKZIEUHZM
      ZWIWLWQWFWLWHWFWLABWJWKDWJAAUIOGUJUKULWIWPIEWIWNEKZWPWOWMUMZPZWIWSMZWPQZW
      MWNUNXAWODUOZPZXCWTXDWOXDWTDUPOUQXBXEWPWFXEWPQZWHWSXDAUSZWFXFABDURXGXEWOA
      PZWFWPXGXEXHXDAWOUTVAWFWPXHWFWMRKZWORKWPXHSWFDRKZXIWFARKXJHABRDVBVCDRVDVE
      WMWNRVFWOARVGVHVIVKVLVJVMVNVOVPVQVRWHWGWRSWFIDWCEWJWKWJTWKTVSVTWAWB $.
      $( [1-Jun-2008] $) $( [22-May-2008] $)
  $}

  ${
    $d A a $.  $d B a f $.  $d F a f $.  $d J a f $.
    mapdiscn2.2 $e |- B = U. J $.
    $( Any mapping whose domain is associated to the discrete topology is
       continuous.  (Contributed by FL, 19-Sep-2011.) $)
    mapdiscn $p |- ( ( J e. Top /\ F : A --> B /\ A e. D ) ->
      F e. ( ~P A Cn J ) ) $=
      ( va vf wcel wf cpw ccn co cv wi wceq imbi12d imbi2d cvv visset ctop feq2
      pweq opreq1d eleq2d wfn ffn fnex feq1 eleq1 mapdiscnlem expcom vtoclg syl
      wa mpan2 mpcom com12 com3l 3imp ) EUAIZABDJZACIZDAKZELMZIZVCVAVBVFVAGNZBD
      JZDVGKZELMZIZOZOVAVBVFOZOGACVGAPZVLVMVAVNVHVBVKVFVGABDUBVNVJVEDVNVIVDELVG
      AUCUDUEQRVHVAVKDVGUFZVHVAVKOZVGBDUGVOVGSIZVHVPOZGTZVOVQUODSIVRVGSDUHVGBHN
      ZJZVAVTVJIZOZOVRHDSVTDPZWAVHWCVPVGBVTDUIWDWBVKVAVTDVJUJRQVAWAWBVGBSVTEHTF
      VSUKULUMUNUPUQURUMUSUT $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.  $d J x $.
    mapudiscn.2 $e |- A = U. J $.
    mapudiscn.3 $e |- B e. _V $.
    $( Any mapping whose range is associated to the undiscrete topology is
       continuous.  (Contributed by FL, 1-Jun-2008.) $)
    mapudiscn $p |- ( ( J e. Top /\ F : A --> B ) ->
      F e. ( J Cn { (/) , B } ) ) $=
      ( vx ctop wcel c0 cpr wf wa cima wceq wi imaeq2 eleq1d syl5ibr cun ccn co
      indistop ccnv cv wral simpr visset elpr ima0 0opn syl5eqel adantrd topopn
      wo fimacnv eleq1 syl impcom jaoi com12 adantlr syl5bi ralrimiv jca wb 0ex
      cuni unipr uncom un0 3eqtrri iscn adantr mpbird mpanl2 ) DHIZJBKZHIZABCLZ
      CDVRUAUBIZBUCVQVSMZVTMZWAVTCUDZGUEZNZDIZGVRUFZMZWCVTWHWBVTUGWCWGGVRWEVRIW
      EJOZWEBOZUOZWCWGWEJBGUHUIVQVTWLWGPVSWLVQVTMZWGWJWMWGPWKWJVQWGVTVQWGWJWDJN
      ZDIVQWNJDWDUJDUKULWJWFWNDWEJWDQRSUMWMWGWKWDBNZDIZVTVQWPVTWOAOZVQWPPABCUPV
      QWPWQADIDAEUNWOADUQSURUSWKWFWODWEBWDQRSUTVAVBVCVDVEWBWAWIVFVTGCDVRABEVRVH
      JBTBJTBJBVGFVIJBVJBVKVLVMVNVOVP $.
      $( [1-Jun-2008] $)
  $}

  ${
    $d J g s v $.  $d X g s v $.
    sallnei.1 $e |- X = U. J $.
    $( Two ways to state the set of all the neighborhoods.  (Contributed by FL,
       2-Aug-2009.) $)
    sallnei $p |- ( J e. Top
                      -> U. ran ( nei ` J )
                         = { v
                           | ( v C_ X /\ E. g e. J g C_ v ) } ) $=
      ( vs ctop wcel cfv cv ciun wss wa wrex cab wceq syl visset elpw c0 eqcomd
      cnei crn cuni cpw neif fniunfv wral crab neival df-rab anbi1i abbii eqtri
      wfn syl6eq ex syl5bi ralrimiv iuneq2 iunab reximi anim2i adantl rexlimiva
      simpr 0ss biantrur rexbii 0elpw sseq1 anbi1d rexbidv rcla4ev mpan sylan2b
      wb anbi2d impbii a1i abbidv syl5eq 3eqtrd ) CGHZCUBIZUCUDZFDUEZFJZWEIZKZF
      WGAJZDLZWHBJZLZWMWKLZMZBCNZMZAOZKZWLWOBCNZMZAOZWDWEWGUOZWFWJPCDEUFXDWJWFF
      WGWEUGUAQWDWIWSPZFWGUHWJWTPWDXEFWGWHWGHZWHDLZWDXEWHDFRSWDXGXEWDXGMWIWQAWG
      UIZWSAWHBCDEUJXHWKWGHZWQMZAOWSWQAWGUKXJWRAXIWLWQWKDARSULUMUNUPUQURUSFWGWI
      WSUTQWDWTWRFWGNZAOXCWRFAWGVAWDXKXBAXKXBVQWDXKXBWRXBFWGWRXBXFWQXAWLWPWOBCW
      NWOVFVBVCVDVEXAWLTWMLZWOMZBCNZXKWOXMBCXLWOWMVGVHVITWGHWLXNMZXKDVJWRXOFTWG
      WHTPZWQXNWLXPWPXMBCXPWNXLWOWHTWMVKVLVMVRVNVOVPVSVTWAWBWC $.
      $( [11-Nov-2013] $) $( [2-Aug-2009] $)
  $}

  ${
    $d J x y z $.  $d X y $.
    nsn.1 $e |- X = U. J $.
    $( The neighborhoods of the singletons are neighborhoods.  (Contributed by
       FL, 2-Aug-2009.) $)
    nsn $p |- ( J e. Top ->
      U_ x e. X ( ( nei ` J ) ` { x } ) C_ U. ran ( nei ` J ) ) $=
      ( vy vz ctop wcel cv csn cnei cfv cuni wrex wi cpw wfn neif wceq syl ciun
      crn cdm eliun visset snelpw fndm eleq2 biimpd eqcoms syl5bi fveq2 rcla4ev
      eleq2d ex syl6 com3l rexlimiv sylbi com12 wfun eqid fnfun elunirn sylibrd
      wb ssrdv ) BGHZEACAIZJZBKLZLZUAZVKUBMZVHEIZVMHZVOFIZVKLZHZFVKUCZNZVOVNHZV
      PVHWAVPVOVLHZACNVHWAOZAVOCVLUDWCWDACVHVICHZWCWAVHWEVJVTHZWCWAOWEVJCPZHZVH
      WFVICAUEUFVHVKWGQZWHWFOZBCDRWIVTWGSWJWGVKUGWJWGVTWGVTSWHWFWGVTVJUHUIUJTTU
      KWFWCWAVSWCFVJVTVQVJSVRVLVOVQVJVKULUNUMUOUPUQURUSUTVHVKVAZWBWAVFVHVKBMZPZ
      QWKBWLWLVBRWMVKVCTFVOVKVDTVEVG $.
      $( [2-Aug-2009] $)
  $}

  ${
    $d A a f $.  $d A f x y $.  $d J a f $.  $d J f x y $.  $d X f x y $.
    osneisi.1 $e |- X = U. J $.
    $( The non empty open sets are neighborhoods of the singletons.
       (Contributed by FL, 16-Jul-2009.) $)
    osneisi $p |- ( ( J e. Top /\ A =/= (/) ) -> ( A e. J ->
       A e. U_ x e. X ( ( nei ` J ) ` { x } ) ) ) $=
      ( vy vf va ctop wcel c0 wne wa cv cfv wrex wss wi wceq ex csn cvv wfn wex
      cnei ciun wral elisset ac5g neeq1 id eleq12d imbi12d rcla4v pm2.27 elunii
      fveq2 cuni eqcomi elssuni syl6sseqr ad2antlr snssi ssid jctir sseq2 sseq1
      eleq2i anbi12d rcla4ev mpan9 adantr jca sneq sseq1d anbi1d anbi2d syl5com
      rexbidva syl5bi mpid syl6 com4l pm2.43a adantl exlimiv 3syl pm2.43i imp31
      wb simpll isnei syl2an mpbird eliun sylibr ) CIJZBKLZMZBCJZBADANZUAZCUEOO
      ZUFJZWSWTMZBXCJZADPZXDXEXGBDQZXBFNZQZXIBQZMZFCPZMZADPZWQWRWTXOWQWRWTXORRZ
      WQCUBJGNZCUCZHNZKLZXSXQOZXSJZRZHCUGZMZGUDWQXPRZCIUHHCGUIYEYFGYDYFXRWTYDWQ
      WRXOYDWTWQWRXORRZWTYDWRBXQOZBJZRZWTYGRYCYJHBCXSBSZXTWRYBYIXSBKUJYKYAYHXSB
      XSBXQUQYKUKULUMUNWRYJWTWQXOWRYJYIWTWQXORZRWRYIUOYIWTYLYIWTMZWQYHCURZJZXOY
      HBCUPYMWQYOXORYOYHDJZYMWQMZXOYNDYHDYNEUSVHYQXHYHUAZXIQZXKMZFCPZMZYPXOYQXH
      UUAWTXHYIWQWTBYNDBCUTEVAVBYMUUAWQYIYRBQZBBQZMZWTUUAYIUUCUUDYHBVCBVDVEWTUU
      EUUAYTUUEFBCXIBSYSUUCXKUUDXIBYRVFXIBBVGVIVJTVKVLVMYPUUBXOXNUUBAYHDXAYHSZX
      MUUAXHUUFXLYTFCUUFXICJZMZXJYSXKUUHXBYRXIUUFXBYRSUUGXAYHVNVLVOVPVSVQVJTVRV
      TTWATWBWCWBWDWCWEWFWGWHWIXEXFXNADXEWQXBDQXFXNWJXADJWQWRWTWKXADVCXBFCBDEWL
      WMVSWNABDXCWOWPT $.
      $( [16-Jul-2009] $)
  $}
  $( The discrete topology on a set ` A ` .  (Contributed by FL,
     19-Sep-2011.) $)
  distopg $p |- ( A e. V -> ~P A e. Top ) $=
    ( wcel cvv cpw ctop elisset c0 cif wceq pweq eleq1d 0ex elimel distop dedth
    syl ) ABCADCZAEZFCZABGRTRAHIZEZFCAHAUAJSUBFAUAKLUAAHDMNOPQ $.
    $( [19-Sep-2011] $)

  ${
    elsubops.1 $e |- K = ( topGen ` ( fi ` S ) ) $.
    $( The elements of a subbase are open sets.  (Contributed by FL,
       16-Apr-2012.) $)
    elsubops $p |- ( S e. A -> S C_ K ) $=
      ( wcel cfi cfv ctg wss ssfii fibas bastg ax-mp sstr sylancl eqcomi sseq2i
      ctb sylib ) BAEZBBFGZHGZIZBCITBUAIUAUBIZUCBAJUAREUDBKUALMBUAUBNOUBCBCUBDP
      QS $.
      $( [14-Dec-2013] $) $( [16-Apr-2012] $)
  $}

