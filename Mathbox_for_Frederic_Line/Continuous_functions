$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Continuous functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x F $.  $d x J $.  $d x K $.  $d x L $.
    $( A mapping remains continuous when the topology associated to its domain
       is replaced by a finer one. $)
    cnrsfin $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( F e. ( J Cn L ) /\ U. J = U. K /\ J C_ K ) -> F e. ( K Cn L ) ) ) $=
      ( vx ctop wcel w3a ccn co cuni wceq wss wf ccnv cv cima wral wa wi wb
      eqid iscn 3adant2 ad2antrr feq2 biimpd adantl ssel ralimdv ad2antlr
      anim12d sylbid exp31 com24 3impd 3adant1 sylibrd ) BFGZCFGZDFGZHZABDIJGZB
      KZCKZLZBCMZHVEDKZANZAOEPQZCGZEDRZSZACDIJGZVBVCVFVGVMVBVGVFVCVMVBVGVFVCVMT
      VBVGSZVFSZVCVDVHANZVJBGZEDRZSZVMVBVCVTUAZVGVFUSVAWAUTEABDVDVHVDUBVHUBZUCU
      DUEVPVQVIVSVLVFVQVITVOVFVQVIVDVEVHAUFUGUHVGVSVLTVBVFVGVRVKEDBCVJUIUJUKULU
      MUNUOUPUTVAVNVMUAUSEACDVEVHVEUBWBUCUQUR $.
      $( [1-Jun-2008] $) $( [22-May-2008] $)
  $}

  ${
    $d x F $.  $d x J $.  $d x K $.  $d x L $.
    $( A mapping remains continuous when the topology associated to its range
       is replaced by a coarser one. $)
    cnrscoa $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( F e. ( J Cn L ) /\ U. L = U. K /\ K C_ L ) ->
         F e. ( J Cn K ) ) ) $=
      ( vx ctop wcel w3a ccn co cuni wceq wss wf ccnv cv cima wral wa wi wb
      eqid iscn 3adant2 ad2antrr feq3 biimpd adantl ssralv ad2antlr anim12d
      sylbid exp31 com24 3impd 3adant3 sylibrd ) BFGZCFGZDFGZHZABDIJGZDKZCKZLZC
      DMZHBKZVDANZAOEPQBGZECRZSZABCIJGZVAVBVEVFVKVAVFVEVBVKVAVFVEVBVKTVAVFSZVES
      ZVBVGVCANZVIEDRZSZVKVAVBVQUAZVFVEURUTVRUSEABDVGVCVGUBZVCUBUCUDUEVNVOVHVPV
      JVEVOVHTVMVEVOVHVCVDVGAUFUGUHVFVPVJTVAVEVIECDUIUJUKULUMUNUOURUSVLVKUAUTEA
      BCVGVDVSVDUBUCUPUQ $.
      $( [1-Jun-2008] $)
  $}


  ${
    $d A x $.  $d B x $.  $d F x $.  $d J x $.
    mapdiscn.1 $e |- F e. C $.
    mapdiscn.2 $e |- B = U. J $.
    mapdiscn.3 $e |- A e. _V $.
    $( mapdiscn.1 is useless. see fnex. $)
    $( Lemma for ~ mapdiscn .  Any mapping whose domain is associated to the
       discrete topology is continuous. $)
    mapdiscnlem $p |- ( ( J e. Top /\ F : A --> B ) ->
      F e. ( ~P A Cn J ) ) $=
      ( vx cpw ctop wcel wf ccn co distop wa cuni ccnv cv cima wral wb eqid
      iscn adantr wceq unipw eqcomi feq23 mp2an biimpi adantl crn wss wi
      imassrn cdm dfdm4 sseq2i fdm sseq2 biimpd cvv fex mpan2 cnvexg syl
      imaexg elpwg 3syl biimprd syl9 mpcom ad2antlr com12 sylbi ax-mp ex
      r19.21aiv mpbir2and mpanl1 ) AJZKLZEKLZABDMZDWCENOLZAHPWDWEQZWFQZWGWCRZER
      ZDMZDSZITZUAZWCLZIEUBZWHWGWLWQQUCWFIDWCEWJWKWJUDWKUDUEUFWFWLWHWFWLAWJUGBW
      KUGWFWLUCWJAAUHUIGABWJWKDUJUKULUMWIWPIEWIWNELZWPWOWMUNZUOZWIWRQZWPUPZWMWN
      UQWTWODURZUOZXBWSXCWOXCWSDUSUIUTXAXDWPWFXDWPUPZWHWRXCAUGZWFXEABDVAXFXDWOA
      UOZWFWPXFXDXGXCAWOVBVCWFWPXGWFWMVDLZWOVDLWPXGUCWFDVDLZXHWFAVDLXIHABVDDVEV
      FDVDVGVHWMWNVDVIWOAVDVJVKVLVMVNVOVPVQVRVSVTWAWB $.
      $( [1-Jun-2008] $) $( [22-May-2008] $)
  $}

  ${
    $d A a $.  $d B a f $.  $d F a f $.  $d J a f $.
    mapdiscn2.2 $e |- B = U. J $.
    $( Any mapping whose domain is associated to the discrete topology is
       continuous. $)
    mapdiscn $p |- ( ( J e. Top /\ F : A --> B /\ A e. D ) ->
      F e. ( ~P A Cn J ) ) $=
      ( va vf ctop wcel wf cpw ccn co cv wi wceq feq2 pweq opreq1d eleq2d
      imbi12d imbi2d wfn ffn cvv visset wa fnex feq1 eleq1 mapdiscnlem expcom
      vtoclg syl mpan2 mpcom com12 com3l 3imp ) EIJZABDKZACJZDALZEMNZJZVCVAVBVF
      VAGOZBDKZDVGLZEMNZJZPZPVAVBVFPZPGACVGAQZVLVMVAVNVHVBVKVFVGABDRVNVJVEDVNVI
      VDEMVGASTUAUBUCVHVAVKDVGUDZVHVAVKPZVGBDUEVOVGUFJZVHVPPZGUGZVOVQUHDUFJVRVG
      UFDUIVGBHOZKZVAVTVJJZPZPVRHDUFVTDQZWAVHWCVPVGBVTDUJWDWBVKVAVTDVJUKUCUBVAW
      AWBVGBUFVTEHUGFVSULUMUNUOUPUQURUNUSUT $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.  $d J x $.
    mapudiscn.2 $e |- A = U. J $.
    mapudiscn.3 $e |- B e. _V $.
    $( Any mapping whose range is associated to the undiscrete topology is
       continuous. $)
    mapudiscn $p |- ( ( J e. Top /\ F : A --> B ) ->
      F e. ( J Cn { (/) , B } ) ) $=
      ( vx ctop wcel c0 cpr wf ccn co indistop wa ccnv cv cima wral wb cuni
      cun 0ex unipr uncom un0 3eqtrri iscn adantr pm3.27 wceq wo wi imaeq2
      eleq1d 0opn ima0 syl5eqel syl5bir adantrd fimacnv eleq1 topopn syl
      impcom jaoi com12 adantlr visset elpr syl5ib r19.21aiv mpbir2and mpanl2
      ) DHIZJBKZHIZABCLZCDVQMNIZBOVPVRPZVSPZVTVSCQZGRZSZDIZGVQTZWAVTVSWGPUAVSGC
      DVQABEVQUBJBUCBJUCBJBUDFUEJBUFBUGUHUIUJWAVSUKWBWFGVQWBWDJULZWDBULZUMZWFWD
      VQIVPVSWJWFUNVRWJVPVSPZWFWHWKWFUNWIWHVPWFVSWHWFWCJSZDIVPWHWEWLDWDJWCUOUPV
      PJDWLDUQWCURUSUTVAWIWFWCBSZDIZWKWIWEWMDWDBWCUOUPVSVPWNVSWMAULZVPWNUNABCVB
      WOWNADIVPWMADVCDAEVDUTVEVFUTVGVHVIWDJBGVJVKVLVMVNVO $.
      $( [1-Jun-2008] $)
  $}

  ${
    $d J g s v $.  $d X g s v $.
    sallnei.1 $e |- X = U. J $.
    $( Two ways to state the set of all the neighborhoods. $)
    sallnei $p |- ( J e. Top
                      -> U. ran ( nei ` J )
                         = { v
                           | ( v C_ X /\ E. g e. J g C_ v ) } ) $=
      ( vs ctop wcel cnei cfv crn cuni cpw cv ciun wss wa wrex cab wfn wceq
      neif fniunfv eqcomd syl wral neival ex visset elpw syl5ib r19.21aiv
      iuneq2 wb pm3.27 reximi anim2i adantl r19.23aiva c0 0elpw sseq1 anbi1d
      rexbidv anbi2d rcla4ev mpan 0ss biantrur rexbii sylan2b impbii a1i
      abbidv iunab syl5eq 3eqtrd ) CGHZCIJZKLZFDMZFNZVSJZOZFWAANZDPZWBBNZPZWGWE
      PZQZBCRZQZASZOZWFWIBCRZQZASZVRVSWATZVTWDUACDEUBWRWDVTFWAVSUCUDUEVRWCWMUAZ
      FWAUFWDWNUAVRWSFWAVRWBDPZWSWBWAHZVRWTWSAWBBCDEUGUHWBDFUIUJUKULFWAWCWMUMUE
      VRWLFWARZASWQWNVRXBWPAXBWPUNVRXBWPWLWPFWAWLWPXAWKWOWFWJWIBCWHWIUOUPUQURUS
      WFUTWGPZWIQZBCRZXBWOUTWAHWFXEQZXBDVAWLXFFUTWAWBUTUAZWKXEWFXGWJXDBCXGWHXCW
      IWBUTWGVBVCVDVEVFVGWIXDBCXCWIWGVHVIVJVKVLVMVNWLFAWAVOVPVQ $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${
    $d J x y z $.  $d X y $.
    nsn.1 $e |- X = U. J $.
    $( The neighborhoods of the singletons are neighborhoods. $)
    nsn $p |- ( J e. Top ->
      U_ x e. X ( ( nei ` J ) ` { x } ) C_ U. ran ( nei ` J ) ) $=
      ( vy vz ctop wcel cv csn cnei cfv ciun crn cuni cdm wrex wi eliun cpw
      wfn neif wceq fndm eleq2 biimpd eqcoms syl visset snelpw syl5ib fveq2
      eleq2d rcla4ev ex syl6 com3l r19.23aiv sylbi com12 wfun wb eqid fnfun
      elunirn sylibrd ssrdv ) BGHZEACAIZJZBKLZLZMZVKNOZVHEIZVMHZVOFIZVKLZHZFVKP
      ZQZVOVNHZVPVHWAVPVOVLHZACQVHWARZAVOCVLSWCWDACVHVICHZWCWAVHWEVJVTHZWCWARVH
      VJCTZHZWFWEVHVKWGUAZWHWFRZBCDUBWIVTWGUCWJWGVKUDWJWGVTWGVTUCWHWFWGVTVJUEUF
      UGUHUHVICAUIUJUKWFWCWAVSWCFVJVTVQVJUCVRVLVOVQVJVKULUMUNUOUPUQURUSUTVHVKVA
      ZWBWAVBVHVKBOZTZUAWKBWLWLVCUBWMVKVDUHFVOVKVEUHVFVG $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  ${
    $d A a f $.  $d A f x y $.  $d J a f $.  $d J f x y $.  $d X f x y $.
    osneisi.1 $e |- X = U. J $.
    $( The non empty open sets are neighborhoods of the singletons. $)
    osneisi $p |- ( ( J e. Top /\ A =/= (/) ) -> ( A e. J ->
       A e. U_ x e. X ( ( nei ` J ) ` { x } ) ) ) $=
      ( vy vf va ctop wcel c0 wne wa cv csn cnei cfv ciun wrex wss wi cvv wfn
      wral wex elisset ac5g wceq neeq1 fveq2 id eleq12d imbi12d rcla4v pm2.27
      cuni elunii sneq adantr sseq1d anbi1d rexbidva anbi2d rcla4ev ex elssuni
      syl6ssr ad2antlr snssi ssid jctir sseq2 sseq1 anbi12d mpan9 jca syl5com
      eqcomi eleq2i syl5ib mpid syl6 com4l pm2.43a adantl 19.23aiv 3syl
      pm2.43i imp31 wb isnei simpll syl2an mpbird eliun sylibr ) CIJZBKLZMZBCJZ
      BADANZOZCPQQZRJZWSWTMZBXCJZADSZXDXEXGBDTZXBFNZTZXIBTZMZFCSZMZADSZWQWRWTXO
      WQWRWTXOUAUAZWQCUBJGNZCUCZHNZKLZXSXQQZXSJZUAZHCUDZMZGUEWQXPUAZCIUFHCGUGYE
      YFGYDYFXRWTYDWQWRXOYDWTWQWRXOUAUAZWTYDWRBXQQZBJZUAZWTYGUAYCYJHBCXSBUHZXTW
      RYBYIXSBKUIYKYAYHXSBXSBXQUJYKUKULUMUNWRYJWTWQXOWRYJYIWTWQXOUAZUAWRYIUOYIW
      TYLYIWTMZWQYHCUPZJZXOYHBCUQYMWQYOXOUAYMWQMZYHDJZXOYOYQXHYHOZXITZXKMZFCSZM
      ZXOYPYQUUBXOXNUUBAYHDXAYHUHZXMUUAXHUUCXLYTFCUUCXICJZMZXJYSXKUUEXBYRXIUUCX
      BYRUHUUDXAYHURUSUTVAVBVCVDVEYPXHUUAWTXHYIWQWTBYNDBCVFEVGVHYMUUAWQYIYRBTZB
      BTZMZWTUUAYIUUFUUGYHBVIBVJVKWTUUHUUAYTUUHFBCXIBUHYSUUFXKUUGXIBYRVLXIBBVMV
      NVDVEVOUSVPVQYNDYHDYNEVRVSVTVEWAVEWBWCWBWDWCWEWFWGWHWIXEXFXNADWQXBDTXFXNW
      JXEXADJXBFCBDEWKWQWRWTWLXADVIWMVBWNABDXCWOWPVE $.
      $( [3-Aug-2009] $) $( [16-Jul-2009] $)
  $}
  $( The discrete topology on a set ` A ` . $)
  distopg $p |- ( A e. B -> ~P A e. Top ) $=
    ( wcel cvv cpw ctop elisset c0 cif wceq pweq eleq1d 0ex elimel distop
    dedth syl ) ABCADCZAEZFCZABGRTRAHIZEZFCAHAUAJSUBFAUAKLUAAHDMNOPQ $.
    $( [19-Sep-2011] $)


