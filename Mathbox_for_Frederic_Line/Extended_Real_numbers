$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Extended Real numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c -e $. $( the negative of an extended real number. $)
  $c +e $. $( Addition of extended real numbers. $)
  $c xe $. $( Multiplication of extended real numbers. $)

  $( Extend class notation to include the negative of an extended number. $)
  clminex $a class -e A $.

  $( Extend class notation to include the negative of an extended number. $)
  claddex $a class +e $.


  $( Define the negative of an extended real number. $)
  df-nege $a |- -e A = if ( A = +oo , -oo , if ( A = -oo , +oo , -u A ) ) $.

  $( Define addition over extended real numbers. $)
  df-adde $a |- +e = { <. <. x , y >. , z >. | ( x e. RR* /\ y e. RR*
   /\ z = if ( A = +oo ,
          if ( B = -oo , ~P RR* , +oo ) ,
       if ( A = -oo ,
          if ( B = +oo , ~P RR* , -oo ) ,
             if ( B = +oo , +oo ,
               if ( B = -oo , -oo , ( A + B ) ) ) ) ) ) } $.

  $( Minus ` +oo ` .  Bourbaki TG IV.15.  (Contributed by FL, 26-Dec-2011.) $)
  mpinf $p |- -e +oo = -oo $=
    ( cpnf clminex wceq cmnf cneg cif df-nege eqid iftrue ax-mp eqtri ) ABAACZD
    ADCAAEFZFZDAGLNDCAHLDMIJK $.
    $( [26-Dec-2011] $)

  $( Minus ` -oo ` .  Bourbaki TG IV.15.  (Contributed by FL, 26-Dec-2011.) $)
  miminf $p |- -e -oo = +oo $=
    ( cmnf clminex cpnf wceq cneg cif df-nege wn wne pnfnemnf df-ne necom mpbir
    bitr3i iffalse eqid iftrue ax-mp syl6eq eqtri ) ABACDZAAADZCAEZFZFZCAGUAHZU
    ECDUFCAIZJUFACIUGACKACLNMUFUEUDCUAAUDOUBUDCDAPUBCUCQRSRT $.
    $( [26-Dec-2011] $)


  $( Minus a real number.  Bourbaki TG IV.15.  (Contributed by FL,
     26-Dec-2011.) $)
  mreal $p |- ( A e. RR -> -e A = -u A ) $=
    ( cr wcel cmnf wne cpnf clminex cneg wceq renemnf renepnf wa cif df-nege wn
    a1i df-ne iffalse sylbi adantl adantr 3eqtrd syl11anc ) ABCADEZAFEZAGZAHZIA
    JAKUDUELZUFAFIZDADIZFUGMZMZUKUGUFULIUHANPUEULUKIZUDUEUIOUMAFQUIDUKRSTUDUKUG
    IZUEUDUJOUNADQUJFUGRSUAUBUC $.
    $( [26-Dec-2011] $)


  ${
    mamb.1 $e |- A = B $.
    $( Equality of two extended numbers with ` -e ` in front of them.
       (Contributed by FL, 26-Dec-2011.) $)
    mamb $p |- -e A = -e B $=
      ( cpnf wceq cif wn wi ax-mp wa iffalse adantl a1i eqcomd adantr 3eqtrd ex
      cmnf pm2.43i iftrue cneg clminex eqeq1 notbid biimpd negeqi syl6bir ecase
      syl6bi sylan9eq mpancom df-nege 3eqtr4i ) ADEZRAREZDAUAZFZFZBDEZRBREZDBUA
      ZFZFZAUBBUBUNUSURVCEZUSGZUNGZVDABEZVFVEHCVGVFVEVGUNUSABDUCUDZUEIVEVFJZURU
      QVBVCVFURUQEZVEUNRUQKZLUQVBEZVIUOUTVLUOGZVLVGVMVMVLHZHCVGVMUTGZVNVGUOUTAB
      RUCUDZVOVMVLVOVMJZUQUPVAVBVMUQUPEZVOUODUPKZLUPVAEZVQABCUFZMVOVAVBEZVMVOVB
      VAUTDVAKNZOPQUIISVOVLVGVOVOVLHZHCVGVOVMWDVPVMVOVLVMVOJZUQUPVAVBVMVRVOVSOV
      TWEWAMVOWBVMWCLPQUGISUOUTUQDVBUODUPTUTVBDUTDVATNUJUHZMVEVBVCEZVFVEVCVBUSR
      VBKNZOPUKVEVDVGVEVEVDHZHCVGVEVFWIVHVFVEVDVFVEJZURUQVBVCVFVJVEVKOVLWJWFMVE
      WGVFWHLPQUGISUNUSURRVCUNRUQTUSVCRUSRVBTNUJUHAULBULUM $.
      $( [26-Dec-2011] $)
  $}

  ${
    nolimf.1 $e |- Y = U. L $.
    nolimf.2 $e |- J = ( topGen ` ran (,) ) $.
    ${
      $d F x $.  $d J x $.  $d L x $.  $d Y x $.
      $( A numerical function has at most one limit value.  (Contributed by FL,
         14-Nov-2010.) $)
      nolimf $p |- ( ( L e. Fil /\ F : Y --> RR )
       -> E* x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cha cr wf cv cflf co cfv wmo cioo crn cuni ctg eqeltri wceq
        rehaus unieqi ctb retopbas unitg ax-mp unirnioo 3eqtrri holimf mp3an2 )
        DHICJIEKBLAMBCDNOPIAQCRSZUAPZJGUDUBABCDKEFCTUOTZUNTZKCUOGUEUNUFIUPUQUCU
        GUNUHUIUJUKULUM $.
        $( [14-Nov-2010] $)
    $}

    ${
      $d F x $.  $d J x $.  $d L x $.  $d Y x $.
      $( A numerical convergent function has one limit value.  (Contributed by
         FL, 14-Nov-2010.) $)
      nolimf2 $p |- ( ( L e. Fil /\ F : Y --> RR /\
       ( ( J fLimf L ) ` F ) =/= (/) )
         -> E! x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cr wf cflf co cfv c0 wne w3a cv wmo weu 3adant3 wb 3ad2ant3
        nolimf n0moeu mpbid ) DHIZEJBKZBCDLMNZOPZQARUIIZASZUKATZUGUHULUJABCDEFG
        UDUAUJUGULUMUBUHAUIUEUCUF $.
        $( [14-Nov-2010] $)
    $}

  $}

  ${

    flimfnein.2 $e |- Y = U. L $.
    flimfnein.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    flimfnein.4 $e |- J = ( topGen ` ran (,) ) $.

    ${
      $d F s $.  $d L s $.  $d N s $.  $d Y s $.
      $( A neighborhood of the limit value ` A ` of a convergent numerical
         function ` F ` contains the image of a filter element.  (Contributed
         by FL, 18-Nov-2010.) $)
      flimfnein $p |- ( ( ( L e. Fil /\ F : Y --> RR )
         /\ ( ( J fLimf L ) ` F ) =/= (/)
         /\ N e. ( ( nei ` J ) ` { A } ) ) -> E. s e. L ( F " s ) C_ N ) $=
        ( cfil wcel cr wf wa cha w3a cfv wceq cuni cflf co c0 wne csn cnei cima
        cv wss wrex cioo crn ctg rehaus eqeltri simpl simpr 3jca unieq uniretop
        a1i syl6req ax-mp flimfneih syl3an1 ) DKLZFMBNZOZCPLZVFVGQBCDUAUBRUCUDE
        AUECUFRRLBGUHUGEUIGDUJVHVIVFVGVIVHCUKULUMRZPJUNUOVAVFVGUPVFVGUQURABCDEM
        FGCVJSZMCTZSJVKVLVJTMCVJUSUTVBVCHIVDVE $.
        $( [18-Nov-2010] $)
    $}

    ${
      $( The limit of a numerical convergent function belongs to ` RR ` .
         (Contributed by FL, 22-Nov-2010.) $)
      limnumrr $p |- ( ( L e. Fil /\ F : Y --> RR /\
       ( ( J fLimf L ) ` F ) =/= (/) )
         -> A e. RR ) $=
        ( cfil wcel cr wf cfv ctop eqeltri a1i cha wceq cuni syl31anc co c0 wne
        cflf w3a cioo crn retop simp1 simp2 rehaus simp3 unieq uniretop syl6req
        ctg ax-mp limvinlv flimfelbas ) DIJZEKBLZBCDUDUAMZUBUCZUEZCNJZUTVAAVBJZ
        AKJVEVDCUFUGUPMZNHUHOPUTVAVCUIZUTVAVCUJZVDCQJZUTVAVCVFVJVDCVGQHUKOPVHVI
        UTVAVCULABCDKECVGRZKCSZRHVKVLVGSKCVGUMUNUOUQZFGURTABCDKEVMFUST $.
        $( [22-Nov-2010] $)
    $}

  $}

  ${
    cinei.1 $e |- J = ( topGen ` ran (,) ) $.
    $( A centered interval is a neighborhood of its center.  (Contributed by
       FL, 18-Nov-2010.) $)
    cinei $p |- ( ( A e. RR /\ B e. RR+ )
      -> ( ( A - B ) (,) ( A + B ) ) e. ( ( nei ` J ) ` { A } ) ) $=
      ( cr wcel crp wa ctop cmin co caddc cioo csn cnei cfv crn ctg retop a1i
      eqeltri iooretop eleqtrri cbci opnneip syl111anc ) AEFBGFHZCIFZABJKZABLKZ
      MKZCFZAUKFUKANCOPPFUHUGCMQRPZIDSUATULUGUKUMCUIUJUBDUCTABUDACUKUEUF $.
      $( [18-Nov-2010] $)
  $}

  ${
    flimfneic.1 $e |- X = U. J $.
    flimfneic.2 $e |- Y = U. L $.
    flimfneic.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    flimfneic.4 $e |- J = ( topGen ` ran (,) ) $.
    flimfneic.5 $e |- N = ( ( A - B ) (,) ( A + B ) ) $.
    $( A centered interval of the limit value ` A ` of a convergent numerical
       function ` F ` contains the image of a filter element.  (Contributed by
       FL, 18-Nov-2010.) $)
    flimfneic $p |- ( ( ( L e. Fil /\ F : Y --> X )
       /\ ( ( J fLimf L ) ` F ) =/= (/)
       /\ B e. RR+ ) -> A e. ( ( J fLimf L ) ` F ) ) $=
      ( cfil wcel wf wa cfv cha cuni cflf co c0 wne crp w3a cioo crn ctg rehaus
      eqeltri a1i simp1l wceq wb unieqi eqtri feq3 ax-mp biimpi adantl 3ad2ant1
      simp2 eqcomi limvinlv syl31anc ) ENOZHGCPZQZCDEUAUBRZUCUDZBUEOZUFZDSOZVGH
      UGUHUIRZTZCPZVKAVJOVNVMDVOSLUJUKULVGVHVKVLUMVIVKVQVLVHVQVGVHVQGVPUNVHVQUO
      GDTZVPIDVOLUPZUQGVPHCURUSUTVAVBVIVKVLVCACDEVPHVRVPVSVDJKVEVF $.
      $( [18-Nov-2010] $)
  $}

  ${
    $d F s $.  $d L s $.  $d N s $.  $d Y s $.
    flimfneicn.2 $e |- Y = U. L $.
    flimfneicn.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    flimfneicn.4 $e |- J = ( topGen ` ran (,) ) $.
    flimfneicn.5 $e |- N = ( ( A - B ) (,) ( A + B ) ) $.
    $( A centered interval of the limit value ` A ` of a convergent numerical
       function ` F ` contains the image of a filter element.  (Contributed by
       FL, 18-Nov-2010.) $)
    flimfneicn $p |- ( ( ( L e. Fil /\ F : Y --> RR )
       /\ ( ( J fLimf L ) ` F ) =/= (/)
       /\ B e. RR+ ) -> E. s e. L ( F " s ) C_ N ) $=
      ( wcel cr wa co cfv ctop cioo eqeltri cfil wf cflf c0 wne crp csn cnei cv
      cima wss wrex wi crn ctg retop a1i simpll simplr cha rehaus cuni uniretop
      unieqi eqtr2i limvinlv mp3anl1 flimfelbas syl31anc ex cmin caddc syl5eqel
      cinei syl6 3imp flimfnein syld3an3 ) EUAMZGNCUBZOZCDEUCPQZUDUEZBUFMZFAUGD
      UHQQZMZCHUIUJFUKHEULWAWCWDWFWAWCANMZWDWFUMWAWCWGWAWCOZDRMZVSVTAWBMZWGWIWH
      DSUNUOQZRKUPTUQVSVTWCURVSVTWCUSDUTMVSVTWCWJDWKUTKVATACDENGDVBWKVBNDWKKVDV
      CVEZIJVFVGACDENGWLIVHVIVJWGWDWFWGWDOFABVKPABVLPSPWELABDKVNVMVJVOVPACDEFGH
      IJKVQVR $.
      $( [18-Nov-2010] $)
  $}

  ${
    lvsovso.1 $e |- Y = U. F $.
    lvsovso.2 $e |- L1 = U. ( ( J fLimf F ) ` F1 ) $.
    lvsovso.3 $e |- L2 = U. ( ( J fLimf F ) ` F2 ) $.
    lvsovso.4 $e |- J = ( topGen ` ran (,) ) $.

    ${
      $d F a r s x $.  $d F1 a r s x $.  $d F2 a r s x $.  $d J r s x $.
      $d L1 r s x $.  $d L2 r s x $.  $d Y r s x $.
      $( If the limit values of two convergent numerical functions are strictly
         ordered, the values of the functions are strictly ordered for some
         element of the filter.  Bourbaki TG IV.18 prop. 2.  (Contributed by
         FL, 6-Dec-2010.) $)
      lvsovso $p |- ( ( ( F e. Fil /\ F1 : Y --> RR /\ F2 : Y --> RR ) /\
           ( ( ( J fLimf F ) ` F1 ) =/= (/) /\
             ( ( J fLimf F ) ` F2 ) =/= (/) /\
             L1 < L2 ) )
         -> E. a e. F A. x e. a ( F1 ` x ) < ( F2 ` x ) ) $=
        ( co wss wa wcel cr w3a wi vr vs cv cima cmin cabs cdiv caddc cioo wrex
        cfv c2 cfil wf cflf c0 wne clt wbr wral crp simpl1 simpl2 jca simpr1 cc
        cc0 limnumrr recnd 3expia com12 3ad2ant2 3adant2 3ad2ant1 3adant3 subcl
        imp syl11anc abscl syl rehalfcl resubcl simpr3 wb posdif mpbid halfpos2
        gt0ne0 absgt0 elrp sylanbrc simpl3 eqid flimfneicn anim12i syl33anc cin
        simpr2 filint inss1 inss2 imass2 mp2an sstr2 im2anan9 ax-mp cuni elunii
        ad2antlr syl6eleqr expcom ffvelrn adantl adantr 3jca wfun cdm ffun wceq
        fdm eqcomd sseq2d biimpcd 3syl impcom 3exp funfvima2 syl6 3ad2ant3 recn
        ancoms syl111anc addcl subadd simpl ssel2 elioooord an4s ex r19.23aiva
        elssuni sseq2 biimpd eqcoms 3impia imp31 halfcl add12 2halves cle jctil
        3imp 0re biimp3a ltle sylc absid eqtr2d eqtrd mpbird simpr breq2 anbi1d
        imbi2d syl5ibr mpd axlttrn r19.21aiv raleq rcla4ev com14 pm2.43i 3expib
        syl7 com23 anabsi5 com3l mpcom ) FUAUCZUDZHIHUENZUFUKZULUGNZUENZHUWCUHN
        ZUINZOZUABUJZGUBUCZUDZIUWCUENZIUWCUHNZUINZOZUBBUJZPZBUMQZDRFUNZDRGUNZSZ
        FCBUONZUKUPUQZGUXAUKUPUQZHIURUSZSZPZAUCZFUKZUXGGUKZURUSZAEUCZUTZEBUJZUX
        FUWQUWRPZUXBUWCVAQZUWQUWSPZUXCUXOUWPUXFUWQUWRUWQUWRUWSUXEVBZUWQUWRUWSUX
        EVCZVDUWTUXBUXCUXDVEUXFUWCRQZVGUWCURUSZUXOUXFUWBRQZUXSUXFUWAVFQZUYAUXFI
        VFQZHVFQZUYBUWTUXEUYCUWQUWSUXEUYCTUWRUXEUXPUYCUXCUXBUXPUYCTUXDUXPUXCUYC
        UWQUWSUXCUYCUWQUWSUXCSIIGCBDJLMVHZVIVJVKVLVKVMVQUWTUXEUYDUWQUWRUXEUYDTU
        WSUXEUXNUYDUXBUXCUXNUYDTUXDUXNUXBUYDUWQUWRUXBUYDUWQUWRUXBSHHFCBDJKMVHZV
        IVJVKVNVKVOVQIHVPZVRZUWAVSZVTZUWBWAZVTZUXFVGUWBURUSZUXTUXFUWAVGUQZUYMUX
        FUWARQZVGUWAURUSZUYNUXFIRQZHRQZUYOUWTUXEUYQUWQUWSUXEUYQTUWRUXEUXPUYQUXC
        UXBUXPUYQTUXDUXPUXCUYQUWQUWSUXCUYQUYEVJVKVLVKVMVQZUWTUXEUYRUWQUWRUXEUYR
        TUWSUXEUXNUYRUXBUXCUXNUYRTUXDUXNUXBUYRUWQUWRUXBUYRUYFVJVKVNVKVOVQZIHWBZ
        VRUXFUXDUYPUWTUXBUXCUXDWCZUXFUYRUYQUXDUYPWDUYTUYSHIWEZVRWFUWAWHVRUXFUYB
        UYNUYMWDUYHUWAWIVTWFUXFUYAUYMUXTWDUYJUWBWGVTWFUWCWJWKZUXFUWQUWSUXQUWQUW
        RUWSUXEWLZVDUWTUXBUXCUXDWRVUDUXNUXBUXOSUWHUXPUXCUXOSUWOHUWCFCBUWFDUAJKM
        UWFWMWNIUWCGCBUWMDUBJLMUWMWMWNWOWPUWHUWOUXFUXMTZUWGUWOVUFTUABUWOUVSBQZU
        WGPZVUFUWNVUHVUFTUBBVUHUWIBQZUWNPVUFVUGVUIUWGUWNVUFVUGVUIPZUWGUWNPZVUFU
        XFVUJVUKUXMUWTUXEVUJVUKUXMTZTZUWQUWRUXFVUMTUWSUWQVUJUXFVULUWQVUGVUIUXFV
        ULTZUWQVUGVUISUVSUWIWQZBQZVUNUVSUWIBWSVUKFVUOUDZUWFOZGVUOUDZUWMOZPZVUPU
        XFUXMVUQUVTOZVUSUWJOZPZVUKVVATVUOUVSOZVUOUWIOZVVDUVSUWIWTUVSUWIXAVVEVVB
        VVFVVCVUOUVSFXBVUOUWIGXBWOXCVVBUWGVURVVCUWNVUTVUQUVTUWFXDVUSUWJUWMXDXEX
        FVUPUXFVVAUXMTTVVAVUPUXFVUPUXMVVAVUPUXFVUPUXMTZTVVAVUPPZUXFUXJAVUOUTZVV
        GVVHUXFVVIVVHUXFPZUXJAVUOVVJUXGVUOQZUXHRQZUWKRQZUXIRQZSZUXHUWKURUSZUWKU
        XIURUSZPZPZUXJVVJVVKVVSVVJVVKPZVVOVVRVVTVVLVVMVVNVVTUWRUXGDQZVVLUXFUWRV
        VHVVKUXRXIVVJVVKVWAVUPVVKVWATVVAUXFVVKVUPVWAVVKVUPPUXGBXGZDUXGVUOBXHJXJ
        XKXIVQZDRUXGFXLVRVVJVVMVVKVVJUYQUXSVVMUXFUYQVVHUYSXMUXFUXSVVHUYLXMIUWCW
        BVRXNVVTUWSVWAVVNUXFUWSVVHVVKVUEXIVWCDRUXGGXLVRXOVVTUXHVUQQZUXIVUSQZPZV
        VRVVHUXFVVKVWFVUPUXFVVKVWFTZTVVAUXFVUPVWGUWTVUPVWGTUXEUWTVUPVVKVWFUWTVU
        PVVKSVWDVWEUWTVUPVVKVWDUWTVUPFXPZVUOFXQZOZPZVVKVWDTUWQUWRVUPVWKTZUWSUWR
        UWQVWLUWRUWQVUPVWKUWRUWQVUPSVWHVWJUWRUWQVWHVUPDRFXRVNUWRVUPVWJUWQVUPUWR
        VWJVUPVUOVWBOZVUODOZUWRVWJTVUOBUUAZDVWBXSVWMVWNTZJVWPVWBDVWBDXSVWMVWNVW
        BDVUOUUBUUCUUDXFZUWRVWNVWJUWRDVWIVUOUWRVWIDDRFXTYAYBYCYDYEVMVDYFYEVOVUO
        UXGFYGYHUULUWTVUPVVKVWEUWTVUPPGXPZVUOGXQZOZVVKVWETUWTVWRVUPUWSUWQVWRUWR
        DRGXRYIXNUWTVUPVWTUWSUWQVUPVWTTUWRVUPUWSVWTVUPVWMVWNUWSVWTTVWOVWQUWSVWN
        VWTUWSDVWSVUOUWSVWSDDRGXTYAYBYCYDVKYIVQVUOUXGGYGVRUUEVDYFXNVKXMUUFVVJVW
        FVVRTZVVKVVHUXFVXAVVAUXFVXATVUPUXFVVAVXAUXFUWKUWEXSZVVAVXATUXFUYRUYQUXD
        VXBUYTUYSVUBUYRUYQUXDSZVXBUWCUWEUHNZIXSZVXCVXDHUWCUWCUHNZUHNZIVXCUWCVFQ
        ZUYDVXHVXDVXGXSUYRUYQVXHUXDUYRUYQPZUYAUWBVFQZVXHVXIUYCUYDPZUYBUYAUYQUYR
        VXKUYQUYCUYRUYDIYJZHYJZWOYKZUYGUYIYDUWBYJZUWBUUGZYDVOZUYRUYQUYDUXDVXMVN
        VXQUWCHUWCUUHYLVXCUWAVXFXSZVXGIXSZVXCVXFUWBUWAVXCVXJVXFUWBXSUYRUYQVXJUX
        DVXIUYBUYAVXJVXIVXKUYBVXNUYGVTZUYIVXOYDVOUWBUUIVTVXCUYOVGUWAUUJUSZUWBUW
        AXSUYRUYQUYOUXDUYQUYRUYOVUAYKZVOVXCVGRQZUYOPZUYPVYAUYRUYQVYDUXDVXIUYOVY
        CVYBUUMUUKVOUYRUYQUXDUYPVUCUUNVGUWAUUOUUPUWAUUQVRUURVXCUYCUYDVXFVFQZSZV
        XRVXSWDUYRUYQVYFUXDVXIUYCUYDVYEUYQUYCUYRVXLXMZUYRUYDUYQVXMXNVXIUXSVXHVX
        HPVYEVXIUYBUYAUXSVXTUYIUYKYDZUXSVXHVXHUWCYJZVYIVDUWCUWCYMYDXOVOIHVXFYNV
        TWFUUSVXCUYCVXHUWEVFQZSZVXBVXEWDUYRUYQVYKUXDVXIUYCVXHVYJVYGVXIUYAVXJVXH
        VXIUYOUYBUYAVYBUWAYJUYIYDVXOVXPYDVXIUYRUXSPUYDVXHPVYJVXIUYRUXSUYRUYQYOV
        YHVDUYRUYDUXSVXHVXMVYIWOHUWCYMYDXOVOIUWCUWEYNVTUUTYLVVAVXAVXBVWFUXHUWEU
        RUSZVVQPZTVVAVWFVYMVURVWDVUTVWEVYMVURVWDPZVYLVUTVWEPZVVQVYNUXHUWFQUWDUX
        HURUSZVYLPVYLVUQUWFUXHYPUXHUWDUWEYQVYPVYLUVAYDVYOUXIUWMQVVQUXIUWLURUSZP
        VVQVUSUWMUXIYPUXIUWKUWLYQVVQVYQYOYDWOYRYSVXBVVRVYMVWFVXBVVPVYLVVQUWKUWE
        UXHURUVBUVCUVDUVEVTVKXNVQXNUVFVDYSVVOVVRUXJUXHUWKUXIUVGVQYHUVHYSVUPVVIU
        XMUXLVVIEVUOBUXJAUXKVUOUVIUVJXKYHYSUVKUVLUVNVTUVMUVOVNUVPUVQVQYRXKYTVKY
        TVQUVR $.
        $( [6-Dec-2010] $)
    $}

    ${
      $d F a x $.  $d F1 a x $.  $d F2 a x $.  $d J a x $.  $d L1 x $.
      $d L2 x $.  $d Y a x $.
      $( Condition on the elements of the filter so that the limits are weakly
         ordered.  Bourbaki TG IV.18 prop. 1.  (Contributed by FL,
         30-Dec-2010.) $)
      lvsovso2 $p |- ( ( ( F e. Fil /\ F1 : Y --> RR /\ F2 : Y --> RR ) /\
           ( ( ( J fLimf F ) ` F1 ) =/= (/) /\
             ( ( J fLimf F ) ` F2 ) =/= (/) /\
             A. a e. F E. x e. a ( F1 ` x ) <_ ( F2 ` x ) ) )
         -> L1 <_ L2 ) $=
        ( cfv wbr w3a wcel cr wi wn cflf co c0 wne cv cle wrex wral cfil wf clt
        lvsovso ex 3com23 3expd com13 3imp wb simp31 simp33 simp2 3jca limnumrr
        cxr rexr simp32 simp1 xrltnle syl11anc bicomd rexnal wa ralnex ad2antrr
        3syl cuni elunii expcom a1i syl6eleqr ffvelrn ralbidva syl5bbr rexbidva
        imp31 jca 3imtr4d con4d 3exp com34 impcom ) FCBUAUBZNUCUDZGWLNUCUDZAUEZ
        FNZWOGNZUFOZAEUEZUGZEBUHZPBUIQZDRFUJZDRGUJZPZHIUFOZWMWNXAXEXFSWMWNXEXAX
        FWMWNXEXAXFSWMWNXEPZXFXAXGIHUKOZWQWPUKOZAWSUHZEBUGZXFTZXATZWMWNXEXHXKSZ
        XEWNWMXNXEWNWMXHXKXBXDXCWNWMXHPZXKSXBXDXCPXOXKABCDEGFIHJLKMULUMUNUOUPUQ
        XGXHXLXGIVDQZHVDQZXHXLURXGXBXDWNPIRQXPXGXBXDWNWMWNXBXCXDUSZWMWNXBXCXDUT
        ZWMWNXEVAVBIGCBDJLMVCIVEVOXGXBXCWMPHRQXQXGXBXCWMXRWMWNXBXCXDVFZWMWNXEVG
        VBHFCBDJKMVCHVEVOIHVHVIVJXMWTTZEBUGXGXKWTEBVKXGYAXJEBYAWRTZAWSUHXGWSBQZ
        VLZXJWRAWSVMYDYBXIAWSYDWOWSQZVLZXIYBYFWQVDQZWPVDQZXIYBURYFXDWODQZVLWQRQ
        YGYFXDYIXGXDYCYEXSVNYFWOBVPZDXGYCYEWOYJQZYCYEYKSSXGYEYCYKWOWSBVQVRVSWEJ
        VTZWFDRWOGWAWQVEVOYFXCYIVLWPRQYHYFXCYIXGXCYCYEXTVNYLWFDRWOFWAWPVEVOWQWP
        VHVIVJWBWCWDWCWGWHWIWJUQWK $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d F a x $.  $d F1 a x $.  $d F2 a x $.  $d J a x $.  $d L1 x $.
      $d L2 x $.  $d Y a x $.
      $( Condition on the values of two numerical functions so that their
         limits are weakly ordered.  Bourbaki TG IV.18 th. 1.  (Contributed by
         FL, 18-Jul-2011.) $)
      lvsovso3 $p |- ( ( ( F e. Fil /\ F1 : Y --> RR /\ F2 : Y --> RR ) /\
           ( ( ( J fLimf F ) ` F1 ) =/= (/) /\
             ( ( J fLimf F ) ` F2 ) =/= (/) /\
             A. x e. Y ( F1 ` x ) <_ ( F2 ` x ) ) )
         -> L1 <_ L2 ) $=
        ( va wcel w3a cfv c0 wne wral wi cfil cr wf cflf co cle wbr wrex simpr1
        cv wa simpr2 cuni wss elssuni eqcomi sseq2i elfilnemp ssralv r19.2z a1i
        ex com13 syl6 com4l 3ad2ant3 syl5com com23 3ad2ant1 com3l pm2.43i com3r
        imp sylbi mpcom com12 r19.21aiv 3jca lvsovso2 syldan ) BUANZDUBEUCZDUBF
        UCZOZECBUDUEZPQRZFWEPQRZAUJZEPWHFPUFUGZADSZOZWFWGWIAMUJZUHZMBSZOGHUFUGW
        DWKUKZWFWGWNWDWFWGWJUIWDWFWGWJULWOWMMBWLBNZWOWMWLBUMZUNZWPWOWMTZWLBUOWR
        WLDUNZWPWSTWQDWLDWQIUPUQWPWOWTWMWPWOWTWMTZTWOWPWPXAWDWKWPWPXATZTZWAWBWK
        XCTWCWAWPWKXBWAWPWKXBTWAWPUKWLQRZWKXBWLBURWJWFXDXBTWGWTWJXDWPWMWTWJWIAW
        LSZXDWPWMTTWIAWLDUSWPXDXEWMXDXEWMTTWPXDXEWMWIAWLUTVBVAVCVDVEVFVGVBVHVIV
        MVJVKVLVNVOVPVQVRABCDMEFGHIJKLVSVT $.
        $( [18-Jul-2011] $)
    $}
  $}


  ${
    $d A x y z $.  $d C x y z $.  $d F w x $.  $d F x y z $.
    $( The supremum of a numerical function ` F ` is greater or equal to every
       element of ` ( F `` A ) ` .  Bourbaki TG IV.20.  (Contributed by FL,
       25-Dec-2011.) $)
    supnuf $p |- ( ( F : A --> RR* /\ A e. _V /\ C e. A ) -> ( F ` C ) <_ ( <_
        supw ran F ) ) $=
      ( vx vy vz cxr cvv wcel cle wbr cv wral wi wa syl 3ad2ant1 wrex syl11anc
      wn vw wf w3a cfv crn cspw cps lteqtpos a1i cdm wfun wceq fdm eleq1 biimpd
      eqcoms imp 3adant3 ffun funrnex sylc wss frn xrex ssex clt xrsupss adantr
      co wb sseld simplr xrlenlt ralbidva con34b xrltnle bicomd rexnal ad2antrr
      simpr rexbidva syl5bbr imbi12d syl5bb anbi12d mpbird domleqt eqcomi spwcl
      biid syl111anc 3jca supdef eqcomd eleq2d biimpa 3adant2 fvelrn rcla4va ex
      breq1 com12 3syl pm2.43i ) AGCUBZAHIZBAIZUCZBCUDZJCUEZUFVIZJKZXHJUGIZXJHI
      ZXKGIZUCDLZXKJKZDXJMZUALXPJKUAXJMXKXPJKNDGMZOXHXLNZXHXMXNXOXMXHUHUIZXHCUJ
      ZHIZCUKZXNXEXFYCXGXEXFYCXEYBAULXFYCNZAGCUMZYEAYBAYBULXFYCAYBHUNUOUPPUQURX
      EXFYDXGAGCUSQZHCUTVAXHXMXNELZXPJKZEXJMZFLZYHJKZFXJMZXPYHJKZNZEGMZOZDGRZXO
      YAXEXFXNXGXEXJGVBZXNAGCVCZXJGVDVEPQXHYRXPYHVFKTZEXJMZYHXPVFKZYHYKVFKZFXJR
      ZNZEGMZOZDGRZXHYSUUIXEXFYSXGYTQZDEFXJVGPXHYQUUHDGXHXPGIZOZYJUUBYPUUGUULYI
      UUAEXJUULYHXJIZOYHGIZUUKYIUUAVJUULUUMUUNUULXJGYHXHYSUUKUUJVHVKUQXHUUKUUMV
      LYHXPVMSVNUULYOUUFEGYOYNTZYMTZNUULUUNOZUUFYMYNVOUUQUUOUUCUUPUUEUUQUUCUUOU
      UQUUNUUKUUCUUOVJUULUUNVTXHUUKUUNVLYHXPVPSVQUUPYLTZFXJRUUQUUEYLFXJVRUUQUUR
      UUDFXJUUQYKXJIZOZUUDUURUUTUUNYKGIZUUDUURVJUULUUNUUSVLUUQUUSUVAXHUUSUVANZU
      UKUUNXEXFUVBXGXEXJGYKYTVKQVSUQYHYKVPSVQWAWBWCWDVNWEWAWFYQDEFXJJHGJUJGWGWH
      ZYQWJWIWKWLDUAXJJHGUVCWMXRXTXSXHXRXLXHXIXJIZXRXLNXHYDBYBIZUVDYGXEXGUVEXFX
      EXGUVEXEAYBBXEYBAYFWNWOWPWQBCWRSUVDXRXLXQXLDXIXJXPXIXKJXAWSWTPXBVHXCXD $.
      $( [25-Dec-2011] $)
  $}


  ${
    $d A x $.  $d C x $.  $d V x $.
    supnufb.1 $e |- F = ( x e. A |-> U ) $.
    supnufb.2 $e |- ( x = C -> U = V ) $.
    $( The supremum of a numerical function ` F ` is greater or equal to every
       element of ` ( F `` A ) ` .  Bourbaki TG IV.20.  (Contributed by FL,
       25-Dec-2011.) $)
    supnufb $p |- ( ( A. x e. A U e. RR* /\ A e. M
       /\ ( C e. A /\ V e. N ) ) -> V <_ ( <_ supw ran F ) ) $=
      ( cxr wcel wral wa w3a cfv cle crn cspw co fvmptg 3ad2ant3 wf cvv fopab2a
      wceq biimpi 3ad2ant1 elisset 3ad2ant2 simp3l supnuf syl111anc eqbrtrrd
      wbr ) DKLABMZBFLZCBLZHGLZNZOZCEPZHQERSTZQUTUPVBHUFUQACDHBGEJIUAUBVABKEUCZ
      BUDLZURVBVCQUOUPUQVDUTUPVDABKDEIUEUGUHUQUPVEUTBFUIUJUPUQURUSUKBCEULUMUN
      $.
      $( [25-Dec-2011] $)
  $}

  ${
    $d A x y z $.
    $( Two ways to express the supremum of a set of extended reals.
       (Contributed by FL, 25-Dec-2011.) $)
    supexr $p |- ( A C_ RR* -> ( <_ supw A ) = sup ( A , RR* , < ) ) $=
      ( vy vx vz cxr cv cle wbr wral wi wa crab cuni wceq wcel wrex lteqtpos wn
      clt wb wss crio cspw co csup wreu cps xrsupss ssel xrlenlt ex com23 imp31
      syl6 ralbidva con34b xrltnle ancoms bicomd adantll rexnal adantr rexbidva
      syl5bbr imbi12d syl5bb anbi12d mpbird biid spweu sylancr riotauni syl cvv
      xrex ssex fldleqt eqcomi spwval2 rabbidva unieqd df-sup syl6reqr 3eqtr4d
      ) AEUAZBFZCFZGHZBAIZDFZWFGHZDAIZWGWFGHZJZBEIZKZCEUBZWPCELZMZGAUCUDZAESUEZ
      WEWPCEUFZWQWSNWEGUGOZWPCEPZXBQWEXDWGWFSHRZBAIZWFWGSHZWFWJSHZDAPZJZBEIZKZC
      EPCBDAUHWEWPXLCEWEWGEOZKZWIXFWOXKXNWHXEBAWEXMWFAOZWHXETZWEXOXMXPWEXOWFEOZ
      XMXPJAEWFUIXQXMXPWFWGUJUKUNULUMUOXNWNXJBEWNWMRZWLRZJXNXQKZXJWLWMUPXTXRXGX
      SXIXMXQXRXGTWEXMXQKXGXRXQXMXGXRTWFWGUQURUSUTXSWKRZDAPXTXIWKDAVAXTYAXHDAXN
      XQWJAOZYAXHTZWEXQYBYCJJXMWEYBXQYCWEYBWJEOZXQYCJAEWJUIYDXQYCYDXQKXHYAXQYDX
      HYATWFWJUQURUSUKUNULVBUMVCVDVEVFUOVGZVCVHWPCBDAGEWPVIVJVKWPCEVLVMWEXCAVNO
      WTWQNQAEVOVPCBDAGVNEGMMEVQVRVSVKWEWSXLCELZMXAWEWRYFWEWPXLCEYEVTWACBDAESWB
      WCWD $.
      $( [20-Nov-2013] $) $( [25-Dec-2011] $)
  $}

  $c sup_ $. $( The supremum of an indexed class. $)
  $( Extend class notation to include the supremum of the class B. $)
  clsupp $a class sup_ x e. A B $.

  $c inf_ $. $( The infimum of an indexed class. $)
  $( Extend class notation to include the infimum of the class B. $)
  clinfp $a class inf_ x e. A B $.

  $( Definition of the supremum of an indexed class of extended reals. $)
  df-supp $a |- sup_ x e. A B = ( <_ supw ( ( x e. A |-> B ) " A ) ) $.

  $( Definition of the infimum of an indexed class of extended reals. $)
  df-infp $a |- inf_ x e. A B = ( <_ infw ( ( x e. A |-> B ) " A ) ) $.

  $( The supremum of a set of extended reals always exists. see supxrcl $)

  ${
    $d A x y $.  $d B y $.
    supbrr.1 $e |- B e. C $.
    $( The supremum of a set of extended reals always exists.  (Contributed by
       FL, 16-Apr-2012.) $)
    supbrr $p |- ( A. x e. A B e. RR* -> sup_ x e. A B e. RR* ) $=
      ( vy cxr wcel wral clsupp cle cmpt cima cspw co df-supp clt wss wceq syl
      csup crn df-mpt elisseti rnssopab wfn cvv elisset ralimi eqid sylib fnima
      mptfng eqcomd sseq1d biimpd syl5bi pm2.43i supxrcl eqeltrd syl5eqel
      supexr ) CGHZABIZABCJKABCLZBMZNOZGABCPVDVGVFGQUAZGVDVFGRZVGVHSVDVIVDVEUBZ
      GRZVDVIAFBGCVEAFBCUCCDEUDUEVDVKVIVDVJVFGVDVFVJVDVEBUFZVFVJSVDCUGHZABIVLVC
      VMABCGUHUIABCVEVEUJUMUKBVEULTUNUOUPUQURZVFVBTVDVIVHGHVNVFUSTUTVA $.
      $( [16-Apr-2012] $)
  $}

  $c Frf $. $( Frechet' s filter. $)

  $( Extends class notation with Frechet's filter. $)
  cfrf $a class Frf $.

  ${
    $d x b $.
    $( Frechet's filter.  Used to define the limit of a sequence. $)
    df-frf $a |- Frf = { x | E. b ( b C_ NN /\ b e. Fin /\ x = ( NN \ b ) ) }
        $.
  $}

