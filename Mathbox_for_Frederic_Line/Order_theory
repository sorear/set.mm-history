$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Order theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c mxl $.
  $c ge $.
  $c ub $.
  $c Preset $.

  $( Extend class notation with the class of all the preorder. $)
  cpreset $a class Preset $.

  $( Extend class notation with a function that returns the maximal elements of
     a preset. $)
  cmxl $a class mxl $.

  $( Extend class notation with a function that returns the greatest element of
     a poset. $)
  cge $a class ge $.

  $( Extend class notation with a function that returns the upper bounds of a
     part of a preset. $)
  cub $a class ub $.

  $( Define the class of all preorders.  A preorder is a transitive and
     reflexive relation. $)
  df-prs $a |- Preset = { r | ( Rel r /\ ( r o. r ) C_ r /\
              ( _I |` U. U. r ) C_ r ) } $.

  ${
    $d R r $.
    $( The predicate "is a preorder". $)
    isprs $p |- ( R e. A -> ( R e. Preset <->
       ( Rel R /\ ( R o. R ) C_ R /\ ( _I |` U. U. R ) C_ R ) ) ) $=
      ( vr cv wrel ccom wss cid cuni cres w3a cpreset wceq releq wb coeq1
      coeq2 eqtrd sseq12 mpancom unieq unieqd reseq2 syl 3anbi123d df-prs
      elab2g ) CDZEZUHUHFZUHGZHUHIZIZJZUHGZKBEZBBFZBGZHBIZIZJZBGZKCBLAUHBMZUIUP
      UKURUOVBUHBNUJUQMVCUKUROVCUJBUHFUQUHBUHPUHBBQRUJUQUHBSTUNVAMZVCUOVBOVCUMU
      TMVDVCULUSUHBUAUBUMUTHUCUDUNVAUHBSTUECUFUG $.
      $( [1-May-2011] $) $( [1-May-2011] $)
  $}

  $( A preorder is a relation. $)
  preorel $p |- ( R e. Preset -> Rel R ) $=
    ( cpreset wcel wrel ccom wss cid cuni cres w3a isprs ibi 3simp1d ) ABCZADZA
    AEAFZGAHHIAFZNOPQJBAKLM $.
    $( [18-May-2011] $) $( [18-May-2011] $)

  ${
    preoref.1 $e |- X = U. U. R $.
    $( A preorder is reflexive. $)
    preoref1 $p |- ( R e. Preset -> ( _I |` X ) C_ R ) $=
      ( cpreset wcel cid cuni cres wrel ccom wss w3a isprs ibi 3simp3d wceq
      reseq2 ax-mp syl5ss ) ADEZFAGGZHZAFBHZTAIZAAJAKZUBAKZTUDUEUFLDAMNOBUAPUCU
      BPCBUAFQRS $.
      $( [18-May-2011] $) $( [18-May-2011] $)

    ${
      $d A x $.  $d R x $.
      $( A preorder is reflexive. $)
      preoref2 $p |- ( ( R e. Preset /\ A e. X ) -> A R A ) $=
        ( vx cpreset wcel wbr cid cres wss wi preoref1 cv cuni wral ref3w wceq
        eqcomi reseq2 ax-mp sseq1i bitri eleq2i wb breq12 anidms rcla4va ex
        sylbi com12 sylbir syl imp ) BFGZACGZAABHZUOICJZBKZUPUQLZBCDMUSENZVABHZ
        EBOOZPZUTVDIVCJZBKUSEBQVEURBVCCRVEURRCVCDSVCCITUAUBUCUPVDUQUPAVCGZVDUQL
        CVCADUDVFVDUQVBUQEAVCVAARVBUQUEVAAVAABUFUGUHUIUJUKULUMUN $.
        $( [22-May-2011] $) $( [22-May-2011] $)
    $}

    ${
      $d R x $.
      $( The domain of a preorder equals its field. $)
      preodom $p |- ( R e. Preset -> dom R = X ) $=
        ( vx cpreset wcel cdm wceq wrel ccom wss cid cuni cres w3a isprs cv
        wbr wral wa domfldref syl6eqr ref3w sylan2br 3adant2 syl6bi pm2.43i )
        AEFZAGZBHZUHUHAIZAAJAKZLAMMZNAKZOUJEAPUKUNUJULUKDQZUOARDUMSZUJUNUKUPTUI
        UMBDAUACUBDAUCUDUEUFUG $.
        $( [22-May-2011] $) $( [22-May-2011] $)
    $}

    ${
      $d R x $.
      $( The range of a preorder equals its field. $)
      preoran $p |- ( R e. Preset -> ran R = X ) $=
        ( vx cpreset wcel cdm crn wrel ccom wss cid cuni cres w3a wceq isprs
        ibi cv wbr wral domrngref ref3w sylan2br 3adant2 syl preodom eqtr3d )
        AEFZAGZAHZBUIAIZAAJAKZLAMMZNAKZOZUJUKPZUIUPEAQRULUOUQUMULDSZURATDUNUAUQ
        UODAUBDAUCUDUEUFABCUGUH $.
        $( [22-May-2011] $) $( [22-May-2011] $)
    $}

    $( If ` <_ ` is a preorder and ` A <_ B ` then ` A ` belongs to the field
       of the preorder. $)
    pre1befi $p |- ( ( R e. Preset /\ A R B ) -> A e. X ) $=
      ( cpreset wcel wbr cdm wceq wi preodom eqcom biimpi eleq2d imbi2d wrel
      preorel releldm ex syl syl5bir mpcom imp ) CFGZABCHZADGZCIZDJZUEUFUGKZCDE
      LUIUJUFAUHGZKZUEUIUGUKUFUIDUHAUIDUHJUHDMNOPUECQZULCRUMUFUKABCSTUAUBUCUD
      $.
      $( [23-May-2011] $) $( [23-May-2011] $)

    $( If ` <_ ` is a preorder and ` A <_ B ` then ` B ` belongs to the field
       of the preorder. $)
    pre2befi $p |- ( ( R e. Preset /\ B e. C /\ A R B ) -> B e. X ) $=
      ( cpreset wcel wbr crn wceq wi preoran eqcom biimpi eleq2d imbi2d wrel
      relelrng 3exp preorel syl5com syl5bir mpcom 3imp ) DGHZBCHZABDIZBEHZDJZEK
      ZUFUGUHUILZLZDEFMUKUMUGUHBUJHZLZLUFUKULUOUGUKUIUNUHUKEUJBUKEUJKUJENOPQQUG
      DRZUOUFUGUPUHUNABCDSTDUAUBUCUDUE $.
      $( [23-May-2011] $) $( [23-May-2011] $)

  $}

  $( A preorder is transitive. $)
  preotr1 $p |- ( R e. Preset -> ( R o. R ) C_ R ) $=
    ( cpreset wcel wrel ccom wss cid cuni cres w3a isprs ibi 3simp2d ) ABCZADZA
    AEAFZGAHHIAFZNOPQJBAKLM $.
    $( [22-May-2011] $) $( [22-May-2011] $)


  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d R x y z $.
    $( A preorder is transitive. $)
    preotr2 $p |- ( ( R e. Preset /\ ( A R B /\ B R C /\ C e. D ) )
      -> A R C ) $=
      ( vx vy vz cpreset wcel wbr w3a ccom wss wi preotr1 cv wa wal cotr wrel
      preorel cvv brrelex wceq 3simp1 3simp2 breq12d 3simp3 anbi12d imbi12d
      cla43gv com23 3exp com12 com4r ex com13 a1dd syl com4l pm2.43i com3l
      com14 3imp sylbi mpcom imp ) EIJZABEKZBCEKZCDJZLZACEKZEEMENZVIVMVNOZEPVOF
      QZGQZEKZVRHQZEKZRZVQVTEKZOZHSGSFSZVIVPOFGHETVIWEVPVIEUAZWEVPOEUBVMWFWEVNV
      JVKVLWFWEVNOZOWFVJVKVLWGWFVJVKVLWGOZOZOVJWFWFWIVJWFWFWIOZOWFVJVJWFWIWFVJV
      JWJOZWFVJRAUCJZWKABEUDVKVJWFWLWHWFVKVJWLWHOZWFVKVJWMOZOVKWFWFWNVKWFWFWNOZ
      OWFVKVKWFWNWFVKVKWOOZWFVKRBUCJZWPBCEUDWQVKWNWFVJVKWQWMVJVKWQWMOWQWLVLVJVK
      RZWGWLWQVLWRWGOZOWLWQVLWSWLWQVLLWEWRVNWDWRVNOFGHABCUCUCDVQAUEZVRBUEZVTCUE
      ZLZWBWRWCVNXCVSVJWAVKXCVQAVRBEWTXAXBUFZWTXAXBUGZUHXCVRBVTCEXEWTXAXBUIZUHU
      JXCVQAVTCEXDXFUHUKULUMUNUOUPUQURUSUTUQVAVBURVBVCVDUTUQVAVBVCVBVAVEVCUTUOV
      FVGVH $.
      $( [23-May-2011] $) $( [23-May-2011] $)
  $}

  $( The composition of a preorder with itself. $)
  altprs2 $p |- ( R e. Preset -> ( R o. R ) = R ) $=
    ( cpreset wcel ccom wceq wrel wss cid cuni cres w3a isprs 3simp2
    reflincror 3adant2 eqssd syl6bi pm2.43i ) ABCZAADZAEZSSAFZTAGZHAIIJAGZKZUAB
    ALUETAUBUCUDMUBUDATGUCANOPQR $.
    $( [13-May-2011] $) $( [13-May-2011] $)

  $( A partial order is a preorder.  (The proof was shortened by Andrew Salmon,
     12-Aug-2011.) $)
  posprs $p |- Poset C_ Preset $=
    ( vr cv wrel ccom wss ccnv cin cid cuni cres wceq w3a cab cps cpreset
    inss1 sseq1 mpbii 3anim3i ss2abi df-ps df-prs 3sstr4i ) ABZCZUDUDDUDEZUDUDF
    ZGZHUDIIJZKZLZAMUEUFUIUDEZLZAMNOUKUMAUJULUEUFUJUHUDEULUDUGPUHUIUDQRSTAUAAUB
    UC $.
    $( [12-Aug-2011] $) $( [1-May-2011] $)

  $( A partial order is a preorder. $)
  posprsOLD $p |- Poset C_ Preset $=
    ( vr cv wrel ccom wss ccnv cin cid cuni cres wceq w3a cab cps cpreset
    inss1 sseq1 mpbiri eqcoms 3anim3i ss2abi df-ps df-prs 3sstr4i ) ABZCZUEUEDU
    EEZUEUEFZGZHUEIIJZKZLZAMUFUGUJUEEZLZAMNOULUNAUKUMUFUGUMUJUIUJUIKUMUIUEEUEUH
    PUJUIUEQRSTUAAUBAUCUD $.
    $( [1-May-2011] $) $( [1-May-2011] $)

  ${
    $d r y a b $.
    $( Define the maximal elements of a set.  I.e. the elements of the set that
       are not smaller than the other elements.  Meaningful is ` r ` is at
       least a preorder.  Read ` ( mxl `` R ) ` as the maximal elements of the
       set ` U. U. R ` preordered by ` R ` .  To have the minimal elements use
       the idiom ` ( mxl `` ``' R ) ` .  Bourbaki E III 8.  Experimental. $)
    df-mxl $a |- mxl = { <. r , y >. |
       y = { a e. U. U. r | A. b e. U. U. r ( a r b -> a = b ) } } $.
  $}

  ${
    $d r x y a b $.
    $( In French, "majorants". $)
    $( Define the upper bounds of a set ` x ` .  Meaningful if ` r ` is at
       least a preorder, and ` x ` a subset of the field of ` r ` .  For the
       lower bounds use the converse relation ` ``' r ` Bourbaki E.III.9 def.
       5.  Experimental. $)
    df-ub $a |- ub = { <. <. r , x >. , y >. |
      y = { a e. U. U. r | A. b e. x b r a } } $.
  $}

  ${
    $d A a b r x y $.  $d R a b r x y $.  $d X a r x y $.
    ubos.1 $e |- X = U. U. R $.
    $( The ` R ` -upper bounds of ` A ` . $)
    ubos $p |- ( ( R e. S /\ A e. B ) -> ( R ub A ) = { a e. X |
      A. b e. A b R a } ) $=
      ( vr vx vy cvv wcel wa cv wbr wral crab cub co wceq cuni unieq unieqd
      rabeq syl breq ralbidv rabbisdv eqcoms ax-mp a1i 3eqtrd raleq1 copab2
      cxp cres df-ub wrel cdm reloprab reldmoprab resid2 mp2an resoprab
      3eqtr2i oprabval2g 3expia elisset anim12i uniexg syl5eqel adantr rabexg
      sylc ) CLMZALMZNGOZFOZCPZGAQZFERZLMZCASTWBUAZCDMZABMZNZVPVQWCWDIJKCALLVRV
      SIOZPZGJOZQZFWHUBZUBZRZWBSVTGWJQZFERZLWHCUAZWNWKFCUBZUBZRZWOFWSRZWPWQWMWS
      UAWNWTUAWQWLWRWHCUCUDWKFWMWSUEUFWQWKWOFWSWQWIVTGWJVRVSWHCUGUHUIXAWPUAZWQE
      WSUAXBHXBWSEWOFWSEUEUJUKULUMWJAUAWOWAFEVTGWJAUNUISKOWNUAZIJKUOZXDLLUPUQZW
      HLMWJLMNXCNIJKUOJKIFGURXDUSXDUTUSXEXDUAXCIJKVAXCIJKVBXDVCVDXCIJKLLVEVFVGV
      HWEVPWFVQCDVIABVIVJWGELMZWCWEXFWFWEWSLEWEWRLMWSLMCDVKWRLVKUFHVLVMWAFELVNU
      FVO $.
      $( [16-May-2011] $) $( [16-May-2011] $)
  $}

  ${
    $d A a b $.  $d R a b $.  $d U a b $.  $d X a $.
    puub.1 $e |- X = U. U. R $.
    $( The predicate " ` U ` is a ` R ` -upper bound of ` A ` ." $)
    puub $p |- ( ( R e. S /\ A e. B ) -> ( U e. ( R ub A ) <-> ( U e. X /\
      A. b e. A b R U ) ) ) $=
      ( va wcel wa cub co cv wbr wral crab ubos eleq2d wceq breq2 ralbidv
      elrab syl6bb ) CDJABJKZECALMZJEGNZINZCOZGAPZIFQZJEFJUGECOZGAPZKUEUFUKEABC
      DFIGHRSUJUMIEFUHETUIULGAUHEUGCUAUBUCUD $.
      $( [16-May-2011] $) $( [16-May-2011] $)
  $}

  ${
    $d A b $.  $d R b $.  $d U b $.  $d V b $.  $d W b $.
    $( If ` <_ ` is a preorder and ` U <_ V ` and ` U ` is an upper bound of
       ` A ` then ` V ` is an upper bound of ` A ` .  Bourbaki E.III.9 nb 8. $)
    prltub2 $p |- ( ( R e. Preset /\ A e. B /\
      ( U e. ( R ub A ) /\ U R V /\ V e. W ) )
         -> V e. ( R ub A ) ) $=
      ( vb cpreset wcel cub co wbr w3a cuni cvv wi eqid fldrels wa cv wral
      pre2befi 3exp com13 imp 3adant1 com12 adantr 3adant2 puub preotr2 expcom
      com14 3imp r19.20dva a1dd com4r adantl syl6bi pm2.43a com4l com3l jca wb
      3ad2ant1 mpbird ex mpid ) CHIZABIZDCAJKZIZDECLZEFIZMZEVKIZVIVJCNNZOIZVOVP
      PZCHVQVQQZRVIVJVRVSPVIVJSZVRVOVPWAVRVOMZVPEVQIZGTZECLZGAUAZSZWBWCWFWAVOWC
      VRWAVOWCVIVOWCPVJVOVIWCVMVNVIWCPZVLVMVNWHVIVNVMWCVIVNVMWCDEFCVQVTUBUCUDUE
      UFUGUHUEUIWAVRVOWFVOWAVRWFVLVMVNWAVRWFPZPWAVLVMVNWIVLWAVMVNWIPPZWAVLDVQIZ
      WDDCLZGAUAZSWAWJPZABCHDVQGVTUJWMWNWKWAVMVNWMWIVIVMVNWMWIPZPPVJVIVMVNWOVIV
      MVNMZWMWFVRWPWLWEGAWPWLWEPZWDAIVIVMVNWQWLVMVNVIWEWLVMVNVIWEPVIWLVMVNMWEWD
      DEFCUKULUCUMUNUHUOUPUCUHUQURUSUTVAUNVBUNVCWAVRVPWGVDVOABCHEVQGVTUJVEVFUCV
      GVHUN $.
      $( [23-May-2011] $) $( [23-May-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d R x $.  $d U x $.
    $( If ` <_ ` is a preorder and ` U ` is an upper bound of ` A ` and
       ` B C_ A ` then ` U ` is an upper bound of ` B ` .  Bourbaki E.III.9 n
       8. $)
    ubpar $p |- ( ( R e. Preset /\ A e. C /\ B C_ A ) -> ( U e. ( R ub A ) ->
       U e. ( R ub B ) ) ) $=
      ( vx cpreset wcel wss w3a cub co cuni cv wbr wral wa wi eqid puub biimpd
      3adant3 simprl ssralv 3ad2ant3 com12 adantl impcom jca wb cvv ssexg ex
      expcom syl6com com3r 3imp adantr mpbird syld ) DGHZACHZBAIZJZEDAKLHZEDMMZ
      HZFNEDOZFAPZQZEDBKLHZVAVBVEVJRVCVAVBQVEVJACDGEVFFVFSZTUAUBVDVJVKVDVJQZVKV
      GVHFBPZQZVMVGVNVDVGVIUCVJVDVNVIVDVNRVGVDVIVNVCVAVIVNRVBVHFBAUDUEUFUGUHUIV
      DVKVOUJZVJVAVBVCVPVBVCVAVPVCVBBUKHZVAVPRVCVBVQBACULUMVAVQVPBUKDGEVFFVLTUN
      UOUPUQURUSUMUT $.
      $( [23-May-2011] $) $( [23-May-2011] $)
  $}

  ${
    $d A x y z $.  $d R x y z $.  $d X x y $.
    supdef.1 $e |- X = dom R $.
    $( If it exists, a ` R ` -supremum of ` A ` is greater or equal to every
       element of ` A ` and is the least element of the upper bounds of
       ` A ` .  Here the existence of the supremum is expressed by the idiom
       ` ( R supw A ) e. X ` . $)
    supdef $p |- ( ( R e. Poset /\ A e. W /\ ( R supw A ) e. X ) ->
       ( A. y e. A y R ( R supw A ) /\
         A. y e. X ( A. z e. A z R y -> ( R supw A ) R y ) ) ) $=
      ( vx cps wcel cspw co w3a cv wbr wral wi wa wrex biid spwex biimp3ar
      wreu spweu ex crab cuni cdm wceq crn psdmrn eleq2 imbi1d ralbidv2 anbi2d
      anbi12d rexbidv2 eqtr rabeq eqcoms eqcom biimpi biidd raleqbidv rabbisdv
      eqtrd unieqd eqeq2d eleq1 biimpd breq2 ralbidv breq1 imbi2d elrab3
      3ad2ant3 syl9 syl6bi com23 syl mpan adantr 3ad2ant1 pm2.43i eqid spwval3
      syl5com 3expia com12 com4t 3adant3 syl6bir 3impia reucl2 syl6 mpd ) DIJZC
      EJZDCKLZFJZMZANZHNZDOZACPZBNXBDOBCPZXCXBDOZQZAFPZRZHFSZXBWSDOZACPZXFWSXBD
      OZQZAFPZRZWQWRXKWTXJHABCDEFGXJTZUAZUBXAXKXQQZWQWRXAXTQWTWQXKXAXQWQXKXJHFU
      CZXAXQQWQXKYAXJHABCDFXRUDUEXAXJHFUFZUGZYBJZXQYAXAYDXQQZWQWRWTXAYEQZWQWRRZ
      WTXKYFXSXAXKYEXAXKYEQZWQWRXAYHQZWTWQDUHZDUGUGZUIZDUJYKUIZRZYIDUKZYLYIYMFY
      JUIZYLYIGXAFYKUIZYHYPYLRZXAYQYHQZWQWRXAYSQWTYQXKYGXAYEYQXKXEXHAYKPZRZHYKS
      ZYGYFQYQXJUUAHFYKYQXCFJXCYKJXJUUAFYKXCULYQXIYTXEYQXHXHAFYKYQXBFJXBYKJXHFY
      KXBULUMUNUOUPUQYGUUBYFWQWRUUBYFXAWSUUAHYKUFZUGZUIZYEWQWRUUBMXAUUEYEQZWQWR
      XAUUFQZWTWQYNUUGYOYLUUGYMYPYLUUGGYRYQUUGFYJYKURZYQUUEXAYEYQUUEWSYCUIZYFYQ
      UUDYCWSYQUUCYBYQUUCUUAHFUFZYBUUCUUJUIYKFUUAHYKFUSUTYQUUAXJHFYQYTXIXEYQXHX
      HAYKFYQYKFUIFYKVAVBYQXHVCVDUOVEVFVGVHUUIYDWSYBJZXAXQYDUUKQYCWSYCWSUIYDUUK
      YCWSYBVIVJUTWTWQUUKXQQWRWTUUKXQXJXQHWSFXCWSUIZXEXMXIXPUULXDXLACXCWSXBDVKV
      LUULXHXOAFUULXGXNXFXCWSXBDVMVNVLUPVOVJVPVQVRVSVTWAWBVTWCWDUUAHABCDIEYKYKW
      EUUATWFWGWHWIVRWJWKWDUUHWGWAWBVTWCWDWIWLWMWDXJHFWNWGWOVSWCWDWP $.
      $( [23-May-2011] $) $( [23-May-2011] $)
  $}

  ${
    $d R a b r y $.  $d X a r y $.
    mxlelt.1 $e |- X = U. U. R $.
    $( The maximal elements of the preorder ` R ` . $)
    mxlelt $p |- ( R e. S -> ( mxl ` R ) = { a e. X | A. b e. X ( a R b
      -> a = b ) } ) $=
      ( vr vy cvv wcel cv wbr wceq wi wral crab cmxl cfv cuni unieq unieqd
      rabeq syl eleq2d breq imbi1d imbi12d ralbidv2 rabbisdv eqcomi ax-mp wb
      eleq2i imbi1i ralbii2 a1i rabbii eqtri 3eqtrd copab cres wa df-mxl wrel
      relopab resid resopab 3eqtr2i fvopab4g elisset uniexg syl5eqel rabexg
      sylanc ) AIJDKZEKZALZVOVPMZNZECOZDCPZIJZAQRWAMABJZGHAVOVPGKZLZVRNZEWDSZSZ
      OZDWHPZWAIIQWDAMZWJWIDASZSZPZVSEWMOZDWMPZWAWKWHWMMWJWNMWKWGWLWDATUAZWIDWH
      WMUBUCWKWIWODWMWKWFVSEWHWMWKVPWHJVPWMJZWFVSWKWHWMVPWQUDWKWEVQVRVOVPWDAUEU
      FUGUHUIWPWAMWKWPWODCPZWAWMCMWPWSMCWMFUJZWODWMCUBUKWOVTDCWOVTULVOCJVSVSEWM
      CWRVPCJVSWMCVPWTUMUNUOUPUQURUPUSQHKWJMZGHUTZXBIVAZWDIJXAVBGHUTHGDEVCXBVDX
      CXBMXAGHVEXBVFUKXAGHIVGVHVIABVJWCCIJWBWCWMICWCWLIJWMIJABVKWLIVKUCFVLVTDCI
      VMUCVN $.
      $( [16-May-2011] $) $( [16-May-2011] $)
  $}

  ${
    $d A a b $.  $d R a b $.  $d X a $.
    ismxl.1 $e |- X = U. U. R $.
    $( The element ` A ` is a maximal element of the preorder ` R ` . $)
    ismxl $p |- ( R e. S -> ( A e. ( mxl ` R ) <->
       ( A e. X /\ A. b e. X ( A R b -> A = b ) ) ) ) $=
      ( va wcel cmxl cfv cv wbr wceq wi wral crab wa mxlelt eleq2d breq1 eqeq1
      imbi12d ralbidv elrab syl6bb ) BCHZABIJZHAGKZEKZBLZUHUIMZNZEDOZGDPZHADHAU
      IBLZAUIMZNZEDOZQUFUGUNABCDGEFRSUMURGADUHAMZULUQEDUSUJUOUKUPUHAUIBTUHAUIUA
      UBUCUDUE $.
      $( [22-May-2011] $) $( [22-May-2011] $)
  $}


  ${
    $d r y z w a $.
    $( Define the greatest element of a poset.  I. e. the (unique if it exists)
       element of the poset that is greater than the other elements of the
       poset.  For the least element use the converse of ` r ` .
       Experimental.  Bourbaki E.R. 25 5 $)
    df-ge $a |- ge = { <. r , y >. | E. z (
      z = { w e. U. U. r | A. a e. U. U. r a r w }
        /\ y = if ( z =/= (/) , U. z , ~P U. U. U. r ) ) } $.
  $}

  ${
    $d A x y $.  $d B x y $.
    inposetlem.1 $e |- A e. _V $.
    inposetlem.2 $e |- B e. _V $.
    inposetlem.3 $e |- C = { <. x , y >. | x C_ y } $.
    $( Lemma for ~ inposet .  Definition of inclusion of sets using a class. $)
    inposetlem $p |- ( A C B <-> A C_ B ) $=
      ( cv wss sseq1 sseq2 brab ) AIZBIZJCOJCDJABCDEFGNCOKODCLHM $.
      $( [23-Sep-2008] $) $( [22-Sep-2008] $)
  $}

  ${
    $d x y $.
    inpc.1 $e |- C = { <. x , y >. | x C_ y } $.
    $( Inclusion is a proper class. $)
    inpc $p |- -. C e. _V $=
      ( cvv wcel cdm vprc cv wss copab wex cab dmeqi dmopab wceq wb abeq1 ssid
      equcomi sseq2d biimpd a4imev ax-mp visset 2th mpgbir 3eqtri eleq1i mtbir
      dmexg mto ) CEFCGZEFZUNEEFHUMEEUMAIZBIZJZABKZGUQBLZAMZECURDNUQABOUTEPUSUO
      EFZQAUSAERUSVAUOUOJZUSUOSVBUQBAUPUOPZVBUQVCUOUPUOBATUAUBUCUDAUEUFUGUHUIUJ
      CEUKUL $.
      $( [23-Sep-2008] $) $( [22-Sep-2008] $)
  $}

  ${
    $d A a b x y z $.  $d C a b x y z $.
    inposet.1 $e |- C = { <. x , y >. | x C_ y } $.
    $( Inclusion partially orders any set. $)
    inposet $p |- ( A e. B -> ( C i^i ( A X. A ) ) e. Poset ) $=
      ( vz va vb wcel cxp cin cps wrel ccom wss ccnv cid cuni cres wceq w3a
      relxp relin2 ax-mp cv wbr wa wex copab wi breq1 sseq1 imbi12d imbi2d
      breq2 sseq2 visset inposetlem sstr2 sylbi com12 chvarv 3ad2ant3 imp
      3simp1 3simp2 anim12i jca cvv wb brinxp2 syl2anb 19.23aiv ssopab2i df-co
      ineq1i df-xp ineq2i inopab 3eqtri 3sstr4i wal asymref uniin uniss sseli
      elin cun unixpss unidm syl6eleq adantl 3syl eqss biimpri expcom a1i
      anbi12i syl5ib id ssid mpbir 3jca mpbird anidmdbi eleq1 anbi12d mpbiri
      impbid 19.21aiv syl mprgbir 3pm3.2i xpexg anidms inex1g incom syl5eqel
      isps ) CDJZECCKZLZMJZYCNZYCYCOZYCPZYCYCQLRYCSZSZTUAZUBZYEYGYJYBNYECCUCEYB
      UDUEAUFZGUFZYCUGZYMBUFZYCUGZUHZGUIZABUJYLYOPZYLCJZYOCJZUHZUHZABUJZYFYCYRU
      UCABYQUUCGYTYMCJZYLYMEUGZUBZUUEUUAYMYOEUGZUBZUUCYNYPUUGUUIUHYSUUBUUGUUIYS
      UUFYTUUIYSUKUUEUUIUUFYSUUHUUEUUFYSUKZUUAUUHHUFZYMEUGZUUKYOPZUKZUKZUUHUUJU
      KHAUUKYLUAZUUNUUJUUHUUPUULUUFUUMYSUUKYLYMEULUUKYLYOUMUNUOYMIUFZEUGZUULUUK
      UUQPZUKZUKUUOIBUUQYOUAZUURUUHUUTUUNUUQYOYMEUPUVAUUSUUMUULUUQYOUUKUQUOUNUU
      RYMUUQPZUUTABYMUUQEGURZIURZFUSUULUVBUUSUULUUKYMPUVBUUSUKABUUKYMEHURZUVCFU
      SUUKYMUUQUTVAVBVAVCVCVDVBVDVEUUGYTUUIUUAYTUUEUUFVFUUEUUAUUHVGVHVIYMVJJYNU
      UGVKUVCYLYMCCEVJVLUEYOVJJYPUUIVKBURYMYOCCEVJVLUEVMVNVOABGYCYCVPYCYSABUJZY
      BLUVFUUBABUJZLUUDEUVFYBFVQYBUVGUVFABCCVRVSYSUUBABVTWAWBYJUUKUUQYCUGZUUQUU
      KYCUGZUHZUUKUUQUAZVKZIWCZHYIHIYCWDUUKYIJZUUKCJZUVMUVNUUKESZYBSZLZSZJUUKUV
      PSZUVQSZLZJZUVOYIUVSUUKYHUVRPYIUVSPEYBWEYHUVRWFUEWGUVSUWBUUKUVPUVQWEWGUWC
      UUKUVTJZUUKUWAJZUHUVOUUKUVTUWAWHUWEUVOUWDUWEUUKCCWIZCUWAUWFUUKCCWJWGCWKWL
      WMVAWNUVOUVLIUVOUVJUVKUVOUVOUUQCJZUUKUUQEUGZUBZUWGUVOUUQUUKEUGZUBZUHZUVKU
      VJUWLUVKUKUVOUWIUWKUVKUWHUVOUWKUVKUKZUWGUWHUUSUWMABUUKUUQEUVEUVDFUSUWKUUS
      UVKUWJUWGUUSUVKUKZUVOUWJUUQUUKPZUWNABUUQUUKEUVDUVEFUSUUSUWOUVKUVKUUSUWOUH
      UUKUUQWOWPWQVAVDVBVAVDVEWRUVHUWIUVIUWKUUQVJJUVHUWIVKUVDUUKUUQCCEVJVLUEUUK
      VJJUVIUWKVKUVEUUQUUKCCEVJVLUEWSWTUVKUVOUVJUVKUVOUVJUKUWGUUQUUQYCUGZUWPUHZ
      UKZUWRUWGUWPUKUWGUWPUWGUWGUUQUUQEUGZUBUWGUWGUWGUWSUWGXAZUWTUWSUWGUWSUUQUU
      QPUUQXBABUUQUUQEUVDUVDFUSXCWRXDUUQUUQCCECVLXEUWGUWPXFXCUVKUVOUWGUVJUWQUUK
      UUQCXGUVKUVHUWPUVIUWPUUKUUQUUQYCULUUKUUQUUQYCUPXHUNXIVBXJXKXLXMXNYAYCVJJZ
      YDYKVKYAYBVJJZUXAYAUXBCCDDXOXPUXBYBELVJYCYBEVJXQEYBXRXSXLVJYCXTXLXI $.
      $( [23-Sep-2008] $) $( [22-Sep-2008] $)
  $}

  ${
    $d F x y $.  $d G x y $.
    definc.1 $e |- G e. X $.
    definc.2 $e |- F e. Y $.
    definc.3 $e |- C = { <. x , y >. | x C_ y } $.
    $( Note: remove definc. 1 using opelxp1. $)
    $( Definition of the inclusion. $)
    definc $p |- ( G ( C i^i ( A X. B ) ) F <->
      ( G e. A /\ F e. B /\ G C_ F ) ) $=
      ( cxp cin wbr wa wss wcel w3a int2rel elisseti inposetlem brxp anbi12i
      simprl simprr pm3.26 3jca 3simp3 3simpa jca impbii 3bitri ) GFECDMZNOGFEO
      ZGFUNOZPGFQZGCRZFDRZPZPZURUSUQSZGFEUNTUOUQUPUTABGFEGHJUAFIKUAZLUBGFCDVCUC
      UDVAVBVAURUSUQUQURUSUEUQURUSUFUQUTUGUHVBUQUTURUSUQUIURUSUQUJUKULUM $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    $d C x $.  $d x y $.
    dominc.1 $e |- C = { <. x , y >. | x C_ y } $.
    $( The domain of the inclusion relation is ` _V ` . $)
    dominc $p |- dom C = _V $=
      ( cdm cvv wceq cv wcel eqv wss wex cab ssid wb sseq2 equcoms biimpd
      a4imev ax-mp abid mpbir copab dmeqi dmopab eqtri eleqtrri mpgbir ) CEZFGA
      HZUIIAAUIJUJUJBHZKZBLZAMZUIUJUNIUMUJUJKZUMUJNUOULBAUKUJGUOULUOULOABUJUKUJ
      PQRSTUMAUAUBUIULABUCZEUNCUPDUDULABUEUFUGUH $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    $d C y $.  $d x y $.
    rninc.1 $e |- C = { <. x , y >. | x C_ y } $.
    $( The range of the inclusion relation is ` _V ` . $)
    rninc $p |- ran C = _V $=
      ( crn cvv wceq cv wcel eqv wss wex cab ssid wb sseq1 equcoms biimpd
      a4imev ax-mp abid mpbir copab rneqi rnopab eqtri eleqtrri mpgbir ) CEZFGB
      HZUIIBBUIJUJAHZUJKZALZBMZUIUJUNIUMUJUJKZUMUJNUOULABUKUJGUOULUOULOBAUJUKUJ
      PQRSTUMBUAUBUIULABUCZEUNCUPDUDULABUEUFUGUH $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    $d A x y $.
    domncnt.1 $e |- C = { <. x , y >. | x C_ y } $.
    $( Domain of the intersection of the inclusion with a square cross
       product. $)
    domncnt $p |- dom ( C i^i ( A X. A ) ) = A $=
      ( cxp cin cdm cv wss copab wcel wa df-xp ineq12i dmeqi inopab simprl
      simprr pm3.26 jca impbii opabbii eqtri wex wral wceq wrex ssid sseq2
      rcla4ev mpan2 df-rex sylib rgen dmopab3 mpbi 3eqtri ) DCCFZGZHAIZBIZJZABK
      ZVACLZVBCLZMZABKZGZHVEVFVCMZMZABKZHZCUTVIDVDUSVHEABCCNOPVIVLVIVCVGMZABKVL
      VCVGABQVNVKABVNVKVNVEVJVCVEVFRVNVFVCVCVEVFSVCVGTUAUAVKVCVGVEVFVCSVKVEVFVE
      VJTVEVFVCRUAUAUBUCUDPVJBUEZACUFVMCUGVOACVEVCBCUHZVOVEVAVAJZVPVAUIVCVQBVAC
      VBVAVAUJUKULVCBCUMUNUOVJABCUPUQUR $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    $d A x y $.
    ranncnt.1 $e |- C = { <. x , y >. | x C_ y } $.
    $( Range of the intersection of the inclusion with a square cross
       product. $)
    ranncnt $p |- ran ( C i^i ( A X. A ) ) = A $=
      ( cxp cin crn cv wss copab wcel wa df-xp ineq12i rneqi incom inopab
      eqtri ccnv cdm df-rn cnvopab dmeqi simplr simpll pm3.27 jca simprl
      pm3.26 simprr impbii opabbii wex wral wceq ssid wrex sseq1 rcla4ev
      ancoms df-rex sylib mpan rgen dmopab3 mpbi 3eqtri ) DCCFZGZHAIZBIZJZABKZV
      KCLZVLCLZMZABKZGZHVQVMMZABKZHZCVJVSDVNVIVREABCCNOPVSWAVSVRVNGWAVNVRQVQVMA
      BRSPWBWATZUAVTBAKZUAZCWAUBWCWDVTABUCUDWEVPVOVMMZMZBAKZUAZCWDWHVTWGBAVTWGV
      TVPWFVOVPVMUEVTVOVMVOVPVMUFVQVMUGUHUHWGVQVMWGVOVPVPVOVMUIVPWFUJUHVPVOVMUK
      UHULUMUDWFAUNZBCUOWICUPWJBCVLVLJZVPWJVLUQWKVPMVMACURZWJVPWKWLVMWKAVLCVKVL
      VLUSUTVAVMACVBVCVDVEWFBACVFVGSVHVH $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${
    $d A r x y $.  $d R r x y $.
    $( Value of an infimum under a weak ordering. $)
    nfwval $p |- ( ( R e. U /\ A e. W ) -> ( R infw A ) = ( `' R supw A ) ) $=
      ( vr vx vy cv ccnv cspw co cinf oprex wceq cnveq opreq1d opreq2 df-nfw
      oprabval5 ) EFGBACDEHZIZFHZJKBIZAJKLUCUBJKUCAJMTBNUAUCUBJTBOPUBAUCJQFGERS
      $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  $( The empty set is a poset. $)
  empos $p |- (/) e. Poset $=
    ( c0 cps wcel wrel ccom wss ccnv cin cid cuni cres wceq w3a rel0 co01
    eqimssi cnv0 ineq2i inidm uni0 unieqi eqtri reseq2 ax-mp res0 eqtr2i
    3eqtri 3pm3.2i cvv wb 0ex isps mpbir ) ABCZADZAAEZAFZAAGZHZIAJZJZKZLZMZUOUQ
    VCNUPAAOPUSAAHAVBURAAQRASVBIAKZAVAALVBVELVAUTAUTATUATUBVAAIUCUDIUEUFUGUHAUI
    CUNVDUJUKUIAULUDUM $.
    $( [7-Oct-2008] $) $( [6-Oct-2008] $)

  $( The converse of a poset is a poset.  In the general case
     ` ( ``' R e. Poset -> R e. Poset ) ` is not true.  See ~ dupos2 for a
     special case where the property holds. $)
  dupos1 $p |- ( R e. Poset -> `' R e. Poset ) $=
    ( cps wcel ccnv wrel ccom wss cin cid cuni cres wceq w3a isps wi relcnv
    a1i cnvss cnvco syl5ssr dfrel2 ineq2 eqeq1d imbi2d wa reseq2 eqeq2d biimpa
    incom syl5eq ex syl5bir sylbi psrel relcnvfld syl sylc 3anim123d sylbid
    pm2.43i cvv wb cnvexg mpbird ) ABCZADZBCZVFEZVFVFFZVFGZVFVFDZHZIVFJJZKZLZMZ
    VEVPVEVEAEZAAFZAGZAVFHZIAJJZKZLZMVPBANVEVQVHVSVJWCVOVQVHOVEVHVQAPQQVSVJOVEV
    SVRDVFVIVRARAASTQVQWAVMLZWCVOOZVEVQVKALZWDWEOAUAWFWEWCVFAHZVNLZOWDWFVOWHWCW
    FVLWGVNVKAVFUBUCUDWDWCWHWDWCUEVTVNWGWDWCVTVNLWDWBVNVTWAVMIUFUGUHVFAUIUJUKUL
    UMAUNZVEVQWDWIAUOUPUQURUSUTVEVFVACVGVPVBABVCVAVFNUPVD $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)

  $( The converse of a poset is a poset. $)
  dupos2 $p |- ( Rel R -> ( R e. Poset <-> `' R e. Poset ) ) $=
    ( wrel cps wcel ccnv dupos1 wceq wi dfrel2 eleq1 biimpd sylbi syl5 impbid2
    ) ABZACDZAEZCDZAFOQEZCDZPROSAGZTPHAIUATPSACJKLQFMN $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)

  ${
    $d x y z $.
    $( The "less than or equal to" relationship on the extended reals is a
       poset. $)
    lteqtpos $p |- <_ e. Poset $=
      ( vx vy vz cle cvv wcel cps cxr cxp clt ccnv cdif df-le xrex xpex difexg
      ax-mp eqeltri wrel ccom wss cin cid cuni cres wceq w3a leqrl cv wbr wa
      wi wal visset xrletr2 ax-gen gen2 cotr mpbir wb asymref fldleqt eleq2i
      mlteqer letri31 biimprd exp3a com23 ancoms syl6 com4l pm2.43i impcom
      com12 eleq1 biimpd ex com3l syl6bi pm2.43a com13 impbid 19.21aiv sylbi
      mprgbir 3pm3.2i isps mpbiri ) DEFZDGFZDHHIZJKZLZEMWKEFWMEFHHNNOWKWLEPQRWI
      WJDSZDDTDUAZDDKUBUCDUDUDZUEUFZUGWNWOWQUHWOAUIZBUIZDUJZWSCUIZDUJUKWRXADUJU
      LZCUMZBUMAUMXCABXBCXAEFXBCUNWRWSXAEUOQUPUQABCDURUSWQWTWSWRDUJZUKZWRWSUFZU
      TZBUMZAWPABDVAWRWPFWRHFZXHWPHWRVBVCXIXGBXIXEXFXEXIXFXDWTXIXFULZXDWTXJULXI
      XDXDWTXFXIXDWSHFZXIUKXDWTXFULULZWSWRHVDXIXKXLXIXKUKZWTXDXFXMWTXDXFXMXFXEW
      RWSVEZVFVGVHVIVJVKVLVMVNXIXFXEULZXFXIXIXEXIXFXIXEULZXFXIXKXFXPULWRWSHVOXI
      XKXFXEXIXKXOXMXFXEXNVPVQVRVSVTWAVLWBWCWDWEWFEDWGWHQ $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  $( A restriction of the identity is a poset. $)
  dispos $p |- ( A e. B -> ( _I |` A ) e. Poset ) $=
    ( wcel cid cres cps wrel ccom wss ccnv cin cuni wceq w3a relres wf wf1o
    f1oi f1of ax-mp fcoi1 eqimssi cnvresid ineq2i inidm cdm crn cun relfld
    dmresi rnresi uneq12i unidm 3eqtrri reseq2 3eqtri 3pm3.2i cvv wb resiexg
    isps syl mpbiri ) ABCZDAEZFCZVEGZVEVEHZVEIZVEVEJZKZDVELLZEZMZNZVGVIVNDAOZVH
    VEAAVEPZVHVEMAAVEQVQARAAVESTAAVEUATUBVKVEVEKVEVMVJVEVEAUCUDVEUEAVLMVEVMMVLV
    EUFZVEUGZUHZAAUHAVGVLVTMVPVEUITVRAVSAAUJAUKULAUMUNAVLDUOTUPUQVDVEURCVFVOUSA
    BUTURVEVAVBVC $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( An idiom to "dereflexivate" a relation. $)
  deref $p |- ( A e. B -> -. A ( C \ _I ) A ) $=
    ( wcel cop cid cdif wn wbr wa ididg df-br sylib notnot intnand eldif
    notbii sylibr ) ABDZAAEZCFGZDZHZAAUAIZHSTCDZTFDZHZJZHUCSUGUESUFUGHSAAFIUFAB
    KAAFLMUFNMOUBUHTCFPQRUDUBAAUALQR $.
    $( [30-Dec-2010] $) $( [30-Dec-2010] $)

  ${
    $d r x $.
    $( Alternate definition of a poset.  Bourbaki E.III.2 prop. 1. $)
    dfps2 $p |- Poset = { r | ( Rel r /\ ( r o. r ) = r /\
              ( r i^i `' r ) = ( _I |` U. U. r ) ) } $=
      ( vx cps cv wrel ccom wss ccnv cin cid cuni cres wceq w3a cab df-ps
      3simp1 wi eqimss2 wa ssin wbr wral ref3w cdm crn eqtr reseq2 eqcoms
      eqeq2d inss1 ssid cores ax-mp sseq1 inclrel coi2 eqss biimpri expcom
      syl6bi syl ex com3l mp2 com23 domrngref domfldref sylc pm2.43a com4l
      sylbir adantr mpcom com13 3imp 3simp3 3jca eqimss 3ad2ant2 impbii abbii
      eqtri ) CADZEZWDWDFZWDGZWDWDHZIZJWDKKZLZMZNZAOWEWFWDMZWLNZAOAPWMWOAWMWOWM
      WEWNWLWEWGWLQWEWGWLWNWLWGWEWNWKWIGZWLWGWEWNRRZWKWISWPWKWDGZWKWHGZTWLWQRZW
      KWDWHUAWRWTWSWRBDZXAWDUBBWJUCZWTBWDUDWEXBWLWGWNXBWEWLWGWNRZRZWEXBWEXDRZWD
      UEZWDUFZMXFWJMZXEWEXBTXHXERXGXFXGXFMZXHXEXIXHTXGWJMZXEXGXFWJUGXJWLWEXCXJW
      LWIJXGLZMZWEXCRZXJWKXKWIWKXKMWJXGWJXGJUHUIUJWIWDGZXKWDFZJWDFZMZXLXMRWDWHU
      KXGXGGXQXGULJWDXGUMUNXLXNXQXMXLXNXKWDGZXQXMRZWIXKWDUOXRXOWFGZXSXKWDWDUPWE
      XTXQXCWEXPWDMZXTXQXCRRWDUQXQYAXTXCXQYAXTXCRZXQYATXOWDMZYBXOXPWDUGYCXTWDWF
      GZXCXOWDWFUOWGYDWNWNWGYDTWFWDURUSUTVAVBVCVDVBVDVBVAVDVEVAVFVBVCUIBWDVGBWD
      VHVIVCVJVKVLVMVLVNVOVPWEWGWLVQVRWOWEWGWLWEWNWLQWNWEWGWLWFWDVSVTWEWNWLVQVR
      WAWBWC $.
      $( [30-Dec-2010] $) $( [30-Dec-2010] $)
  $}

  ${
    $d A x y $.  $d R x y $.  $d R x z $.
    $( Any subset of a partially ordered set is partially ordered. $)
    pospos $p |- ( R e. Poset -> ( R i^i ( A X. A ) ) e. Poset ) $=
      ( vx vy vz cps wcel cxp cin wrel ccom wss ccnv cid cuni cres wceq w3a
      isps relin1 tricptr cv wbr wral wa wi wal asymref2 df-ral uuniin ssel
      elin visset brxp pm2.27 int2rel biimpri ex syl6 com3r sylbir fldsqcp2
      eleq2i sylancb impcom sylbi ax-mp com12 a4s adantr r19.21aiv expcom imp
      19.20i2 adantl jca sylibr 3anim123i syl6bi pm2.43i cvv wb inex1g syl
      mpbird ) BFGZBAAHZIZFGZWHJZWHWHKWHLZWHWHMINWHOOZPQZRZWFWNWFWFBJZBBKBLZBBM
      INBOOZPQZRWNFBSWOWJWPWKWRWMBWGTABUAWRCUBZWSWHUCZCWLUDZWSDUBZWHUCZXBWSWHUC
      ZUEZWSXBQZUFZDUGCUGZUEWMWRXAXHWRWTCWLWRWSWSBUCZCWQUDZWSEUBZBUCXKWSBUCUEWS
      XKQUFEUGCUGZUEWSWLGZWTUFZCEBUHXJXNXLXJWSWQGZXIUFZCUGXNXICWQUIXPXNCXMXPWTW
      LWQWGOOZIZLZXMXPWTUFZUFBWGUJXSXMWSXRGZXTWLXRWSUKYAXOWSXQGZUEXTWSWQXQULYBX
      OXTWSAGZYCXOXTUFZYBYCYCUEWSWSWGUCZYDWSWSAACUMUNXOXPYEWTXOXPXIYEWTUFXOXIUO
      XIYEWTWTXIYEUEWSWSBWGUPUQURUSUTVAXQAWSAVBVCZYFVDVEVFUSVGVHVIVFVJVFVKWRXJW
      SXBBUCZXBWSBUCZUEZXFUFZDUGCUGZUEXHCDBUHYKXHXJYJXGCDXEYJXFXCXDYJXFUFZXCYGW
      SXBWGUCZUEXDYLUFZWSXBBWGUPYGYNYMXDYGYLXDYHXBWSWGUCZUEYGYLUFZXBWSBWGUPYHYP
      YOYGYHYLYIXFUOVLVJVFVHVJVFVMVHVNVOVFVPCDWHUHVQVRVSVTWFWHWAGWIWNWBBWGFWCWA
      WHSWDWE $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${
    $d A x y $.  $d R x y $.
    $( Any subset of a totally ordered set is totally ordered. $)
    tostos $p |- ( R e. Toset -> ( R i^i ( A X. A ) ) e. Toset ) $=
      ( vx vy ccha wcel cxp cin cps cv wbr wo cuni wral wa eqid istoset biimpd
      pospos wi wss uuniin ssel2 elin w3a ax-17 hbra1 hbim hb3an pm2.27 ra4
      a1d syl6 3imp sseli pm3.26bi syl syl5 imp fldsqcp2 eleq2i biimpi
      3ad2ant3 adantl sylbi anim12i visset brxp sylibr jca andir sylib sqpeq
      ersymb anbi2i orbi2i int2rel orbi12i ex r19.21ai 3exp com23 mpan com12
      r19.20i2 adantr cvv wb inex1g ad2antrr mpbird syli pm2.43i ) BEFZBAAGZHZE
      FZWNWNBIFZCJZDJZBKZWTWSBKZLZDBMMZNZCXDNZOZWQWNWNXGCDEBXDXDPQRXGWNWQXGWNOZ
      WQWPIFZWSWTWPKZWTWSWPKZLZDWPMMZNZCXMNZOZXGXPWNWRXIXFXOABSXEXNCXDXMWSXMFZW
      SXDFZXETZXNXMXDWOMMZHZUAZXQXSXNTZBWOUBZYBXQOWSYAFZYCXMYAWSUCYEXRWSXTFZOYC
      WSXDXTUDXRYFYCXRXSYFXNXRXSYFXNXRXSYFUEZXLDXMXRXSYFDXRDUFZXRXEDYHXCDXDUGUH
      YFDUFUIYGWTXMFZXLYGYIOZXAWSWTWOKZOZXBWTWSWOKZOZLZXLYJYLXBYKOZLZYOYJXCYKOY
      QYJXCYKYGYIXCYGWTXDFZXCYIXRXSYFYRXCTZXRXSXEYFYSTXRXEUJXEYSYFXCDXDUKULUMUN
      YIWTYAFZYRXMYAWTYDUOZYTYRWTXTFZWTXDXTUDZUPUQURUSYJWSAFZWTAFZOYKYGUUDYIUUE
      YFXRUUDXSYFUUDXTAWSAUTZVAVBVCYIYTUUEUUAYTYRUUBOUUEUUCUUBUUEYRUUBUUEXTAWTU
      UFVAVBVDVEUQVFWSWTAADVGZVHVIVJXAXBYKVKVLYPYNYLYKYMXBWSWTWOCVGUUGAVMVNVOVP
      VLXJYLXKYNWSWTBWOVQWTWSBWOVQVRVIVSVTWAWBUSVEUQWCWDWEVFWFXHWPWGFZWQXPWHWRU
      UHXFWNBWOIWIWJCDWGWPXMXMPQUQWKVSWLWM $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}


  ${
    $d C a u v x y $.  $d J a u v x y $.
    toplat.1 $e |- C = { <. u , v >. | u C_ v } $.
    $( A topology when ordered by the inclusion is a lattice.  This fact leads
       to the idea of pointless topology, that is a lattice looked at with the
       eyes of a topologist. $)
    toplat $p |- ( J e. Top -> ( C i^i ( J X. J ) ) e. Lat ) $=
      ( vx vy va ctop wcel cxp cin cps cv cpr cspw co cinf wa wral cla inposet
      cuni cvv wbr wi wceq cdm domncnt eqcomi spwpr4 adantr zfpair uniex jctir
      wss w3a simprl uniopn visset prss sylan2b prid1 a1i ssid jctil ssuni syl
      3jca definc sylibr simprr prid2 jca cun unss ad2antrl unipr sseq1i
      biimpi ex sylbi expcom 3ad2ant3 com12 imp r19.21aiva syl3anc eqeltrd
      ccnv cint nfwval crn ranncnt df-rn eqtr3i dupos1 c0 wne prnz intex mpbi
      intpr eleq1i adantl pm3.26 inss1 inex1 syl5eqbr brcnvg mpbird adantlr
      pm3.27 inss2 eqsstri adantll inopn 3expb syl5eqel sylanc simplr ad2antrr
      ssin syl6ssr syl2ani wb sylan biimpd syl2and pm3.2i exp31 r19.21aiv
      3eqtrd r19.21aivv isla ) DIJZCDDKLZMJZYQFNZGNZOZPQZDJZYQUUARQZDJZSZGDTFDT
      ZSYQUAJYPYRUUGBADICEUBZYPUUFFGDDYPYSDJZYTDJZSZUUFYPUUKSZUUCUUEUULUUBUUAUC
      ZDYRUUMUDJZSYSUUMYQUEZYTUUMYQUEZSYSHNZYQUEZYTUUQYQUEZSZUUMUUQYQUEZUFZHDTU
      UBUUMUGUULHYSYTUUMUDYQDYQUHDBADCEUIUJZUKUULYRUUNYPYRUUKUUHULZUUAFGUMZUNZU
      OUULUUOUUPUULUUIUUMDJZYSUUMUPZUQUUOUULUUIUVGUVHYPUUIUUJURZYPUUADUPUVGUUKU
      UADUSYSYTDFUTZGUTZVAVBZUULYSYSUPZYSUUAJZSUVHUULUVNUVMUVNUULYSYTUVJVCVDYSV
      EVFYSYSUUAVGVHVIBADDCUUMYSUDUDUVJUVFEVJVKUULUUJUVGYTUUMUPZUQUUPUULUUJUVGU
      VOYPUUIUUJVLZUVLUULYTYTUPZYTUUAJZSUVOUULUVRUVQUVRUULYSYTUVKVMVDYTVEVFYTYT
      UUAVGVHVIBADDCUUMYTUDUDUVKUVFEVJVKVNUULUVBHDUUTUULUUQDJZSZUVAUURUUSUVTUVA
      UFZUURUUIUVSYSUUQUPZUQUUSUWAUFZBADDCUUQYSUDUDUVJHUTZEVJUWBUUIUWCUVSUUSUWB
      UWAUUSUUJUVSYTUUQUPZUQUWBUWAUFZBADDCUUQYTUDUDUVKUWDEVJUWEUUJUWFUVSUWBUWEU
      WAUWBUWESYSYTVOZUUQUPZUWAYSYTUUQVPUWHUVTUVAUWHUVTSZUVGUVSUUMUUQUPZUQUVAUW
      IUVGUVSUWJUULUVGUWHUVSUVLVQUWHUULUVSVLUWHUWJUVTUWHUWJUWGUUMUUQUUMUWGYSYTU
      VJUVKVRUJVSVTULVIBADDCUUQUUMUDUDUVFUWDEVJVKWAWBWCWDWBWEWDWBWFWEWGWHUVLWIU
      ULUUDYSYTLZDUULUUDYQWJZUUAPQZUUAWKZUWKUULYRUUAUDJZSUUDUWMUGUULYRUWOUVDUVE
      UOUUAYQMUDWLVHUWLMJZUWNUDJZSYSUWNUWLUEZYTUWNUWLUEZSZYSUUQUWLUEZYTUUQUWLUE
      ZSZUWNUUQUWLUEZUFZHDTUWMUWNUGUULHYSYTUWNUDUWLDYQWMDUWLUHBADCEWNYQWOWPUKUU
      LUWPUWQYPUWPUUKYPYRUWPUUHYQWQVHULUUAWRWSUWQYSYTUVJWTUUAXAXBZUOUUKUWNDJZUW
      TUULUUKUXGSUWRUWSUUIUXGUWRUUJUUIUXGSZUWRUWNYSYQUEUXHUWKYSUWNYQUXHUWKDJZUU
      IUWKYSUPZUQUWKYSYQUEUXHUXIUUIUXJUXGUXIUUIUXGUXIUWNUWKDYSYTUVJUVKXCZXDVTXE
      UUIUXGXFUXJUXHYSYTXGVDVIBADDCYSUWKUDUDYSYTUVJXHUVJEVJVKUXKXIYSUWNDDYQXJXK
      XLUUJUXGUWSUUIUUJUXGSZUWSUWNYTYQUEZUXLUXGUUJUWNYTUPZUQUXMUXLUXGUUJUXNUUJU
      XGXMUUJUXGXFUXNUXLUWNUWKYTUXKYSYTXNXOVDVIBADDCYTUWNUDUDUXFUVKEVJVKYTUWNDD
      YQXJXKXPVNYPUUKXMUULUWKDUWNYPUUIUUJUXIYSYTDXQXRZUXKXSZXTUULUXEHDUULUVSUXC
      UXDUVTUXCSZUXDUUQUWNYQUEZUVTUXCUXRUVTUUQYSYQUEZUUQYTYQUEZUXRUXAUXBUVTUVSU
      UIUUQYSUPZUQZUVSUUJUUQYTUPZUQZUXRUXSUXTUVTUYBUYDSZUXRUVTUYESZUVSUXGUUQUWN
      UPZUQUXRUYFUVSUXGUYGUULUVSUYEYAUULUXGUVSUYEUXPYBUYEUYGUVTUYBUYDUYGUYAUVSU
      YDUYGUFUUIUYDUYAUYGUYCUVSUYAUYGUFUUJUYAUYCUYGUYAUYCSZUUQUWKUWNUYHUUQUWKUP
      UUQYSYTYCVTUXKYDWCWDWEWDWFXEVIBADDCUWNUUQUDUDUWDUXFEVJVKWAUXSUYBBADDCYSUU
      QUDUDUWDUVJEVJVTUXTUYDBADDCYTUUQUDUDUWDUVKEVJVTYEUVTUXAUXSUUIUVSUXAUXSYFU
      ULYSUUQDDYQXJUVIYGYHUVTUXBUXTUUJUVSUXBUXTYFUULYTUUQDDYQXJUVPYGYHYIWFUXQUW
      QUUQUDJZSZUXDUXRYFUYJUXQUWQUYIUXFUWDYJVDUWNUUQUDUDYQXJVHXKYKYLWHUWNUWKUGU
      ULUXKVDYMUXOWIVNWAYNVNFGYQDUVCYOVK $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}


