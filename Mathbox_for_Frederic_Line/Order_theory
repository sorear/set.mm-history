$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Order theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Toset $.
  $c mxl $.
  $c mnl $.
  $c ge $.
  $c le $.

  $( Extend class notation with the class of all totally ordered sets. $)
  ccha $a class Toset $.

  $( Extend class notation with a function that returns the maximal
     elements of a part of a poset. $)
  cmxl $a class mxl $.

  $( Extend class notation with a function that returns the minimal
     elements of a part of a poset. $)
  cmnl $a class mnl $.

  $( Extend class notation with a function that returns the greatest
     element of a part of a poset. $)
  cge $a class ge $.

  $( Extend class notation with a function that returns the least
     element of a part of a poset. $)
  cse $a class le $.

  ${ $d r x y $.
    $( Define the class of all totally ordered sets. $)
    df-toset $a |- Toset = ( Poset i^i { r  | A. x e. U. U. r A. y e. U. U. r
      ( x r y \/ y r x ) } ) $.
  $}

  ${ $d R r x y $. $d X r x y $.
    istoset.1 $e |- X = U. U. R $.
    $( The predicate is a toset. $)
    istoset $p |- ( R e. A -> ( R e. Toset <->
      ( R e. Poset /\ A. x e. X A. y e. X ( x R y \/ y R x ) ) ) ) $=
      ( vr wcel cps cv wbr wo cuni wral cab cin wa ccha wceq unieq unieqd breq
      orbi12d raleq12d eqcomi a1i raleq1d bitrd elabg anbi2d elin syl5bb
      df-toset eleq2i ) DCHZDIAJZBJZGJZKZUQUPURKZLZBURMZMZNZAVCNZGOZPZHZDIHZUPU
      QDKZUQUPDKZLZBENZAENZQZDRHUOVIDVFHZQVOVHUOVPVNVIVEVNGDCURDSZVEVLBDMZMZNZA
      VSNVNVQVDVTAVCVSVQVBVRURDTUAZVQVAVLBVCVSWAVQUSVJUTVKUPUQURDUBUQUPURDUBUCU
      DUDVQVTVMAVSEVSESVQEVSFUEUFZVQVLBVSEWBUGUDUHUIUJDIVFUKULRVGDABGUMUNUL $.
      $( [3-Nov-2009] $) $( [1-Nov-2009] $)
  $}


  ${ $d r x y a b $.
    $( Define the maximal elements of a part of a poset. I.e. the elements of
       the part that are not smaller than the other elements of the part. Read
       ` R mxl A ` as the ` R ` -maximal elements of set ` A ` .  Experimental.
       Don't use. Bourbaki E.R.28 6. $)
    df-mxl $a |- mxl = { <. <. r , x >. , y >. |
       y = { a e. x | A. b e. x -. ( a r b /\ a =/= b ) } } $.
  $}

  ${ $d r x y $.
    $( Define the minimal elements of a part of a poset. I.e. the elements of
       the part that are not greater than the other elements of the part. Read
       ` R mnl A ` as the ` R ` -minimal elements of set ` A ` .  Experimental.
       Don't use. Bourbaki E.R.28 6. $)
    df-mnl $a |- mnl = { <. <. r , x >. , y >. | y = ( `' r mxl x ) } $.
  $}

  ${ $d r x y z w a $.
    $( Define the greatest element of a part of a poset. I. e. the (unique if
       it exists) element of a part that is greater than the other elements of
       the part. Experimental. Don't use. Bourbaki E.R. 25 5 $)
    df-ge $a |- ge = { <. <. r , x >. , y >. | E. z (
      z = { w e. x | A. a e. x a r w }
        /\ y = if ( z =/= (/) , U. z , P~ U. U. U. r ) ) } $.
  $}


  ${ $d r x y $.
    $( Define the least element of a part of a poset. I. e. the (unique if
       it exists) element of a part that is less than the other elements of
       the part. Experimental. Don't use. Bourbaki E.R. 25 5 $)
    df-lea $a |- le = { <. <. r , x >. , y >. | y = ( `' r ge x ) } $.
  $}


  ${ $d A x y $. $d B x y $.
    inposetlem.1 $e |- A e. V $.
    inposetlem.2 $e |- B e. V $.
    inposetlem.3  $e |- C = { <. x , y >. | x (_ y } $.
    $( Lemma for ~ inposet . Definition of inclusion of sets using a class. $)
    inposetlem $p |- ( A C B <-> A (_ B ) $=
      ( cv wss sseq1 sseq2 brab ) AIZBIZJCOJCDJABCDEFGNCOKODCLHM $.
      $( [23-Sep-2008] $) $( [22-Sep-2008] $)
  $}

  ${ $d x y $.
    inpc.1  $e |- C = { <. x , y >. | x (_ y } $.
    $( Inclusion is a proper class. $)
    inpc $p |- -. C e. V $=
      ( cvv wcel cdm vprc cv wss copab wex cab dmeqi dmopab wceq wb abeq1 ssid
      equcomi sseq2d biimpd a4imev ax-mp visset 2th mpgbir 3eqtri eleq1i mtbir
      dmexg mto ) CEFCGZEFZUNEEFHUMEEUMAIZBIZJZABKZGUQBLZAMZECURDNUQABOUTEPUSUO
      EFZQAUSAERUSVAUOUOJZUSUOSVBUQBAUPUOPZVBUQVCUOUPUOBATUAUBUCUDAUEUFUGUHUIUJ
      CEUKUL $.
      $( [23-Sep-2008] $) $( [22-Sep-2008] $)
  $}

  ${ $d A a b x y z $. $d C a b x y z $.
    inposet.1  $e |- C = { <. x , y >. | x (_ y } $.
    $( Inclusion partially orders any set. $)
    inposet $p |- ( A e. B -> ( C i^i ( A X. A ) ) e. Poset ) $=
      ( vz va vb wcel cxp cin cps wrel ccom wss ccnv cid cuni cres wceq w3a
      relxp relin2 ax-mp cv wbr wa wex copab wi breq1 sseq1 imbi12d imbi2d
      breq2 sseq2 visset inposetlem sstr2 sylbi com12 chvarv 3ad2ant3 imp
      3simp1 3simp2 anim12i jca cvv wb brinxp2 syl2anb 19.23aiv ssopab2i df-co
      ineq1i df-xp ineq2i inopab 3eqtri 3sstr4i wal asymref uniin uniss sseli
      elin cun unixpss unidm syl6eleq adantl 3syl eqss biimpri expcom a1i
      anbi12i syl5ib id ssid mpbir 3jca mpbird anidmdbi eleq1 anbi12d mpbiri
      impbid 19.21aiv syl mprgbir 3pm3.2i xpexg anidms inex1g incom syl5eqel
      isps ) CDJZECCKZLZMJZYCNZYCYCOZYCPZYCYCQLRYCSZSZTUAZUBZYEYGYJYBNYECCUCEYB
      UDUEAUFZGUFZYCUGZYMBUFZYCUGZUHZGUIZABUJYLYOPZYLCJZYOCJZUHZUHZABUJZYFYCYRU
      UCABYQUUCGYTYMCJZYLYMEUGZUBZUUEUUAYMYOEUGZUBZUUCYNYPUUGUUIUHYSUUBUUGUUIYS
      UUFYTUUIYSUKUUEUUIUUFYSUUHUUEUUFYSUKZUUAUUHHUFZYMEUGZUUKYOPZUKZUKZUUHUUJU
      KHAUUKYLUAZUUNUUJUUHUUPUULUUFUUMYSUUKYLYMEULUUKYLYOUMUNUOYMIUFZEUGZUULUUK
      UUQPZUKZUKUUOIBUUQYOUAZUURUUHUUTUUNUUQYOYMEUPUVAUUSUUMUULUUQYOUUKUQUOUNUU
      RYMUUQPZUUTABYMUUQEGURZIURZFUSUULUVBUUSUULUUKYMPUVBUUSUKABUUKYMEHURZUVCFU
      SUUKYMUUQUTVAVBVAVCVCVDVBVDVEUUGYTUUIUUAYTUUEUUFVFUUEUUAUUHVGVHVIYMVJJYNU
      UGVKUVCYLYMCCEVJVLUEYOVJJYPUUIVKBURYMYOCCEVJVLUEVMVNVOABGYCYCVPYCYSABUJZY
      BLUVFUUBABUJZLUUDEUVFYBFVQYBUVGUVFABCCVRVSYSUUBABVTWAWBYJUUKUUQYCUGZUUQUU
      KYCUGZUHZUUKUUQUAZVKZIWCZHYIHIYCWDUUKYIJZUUKCJZUVMUVNUUKESZYBSZLZSZJUUKUV
      PSZUVQSZLZJZUVOYIUVSUUKYHUVRPYIUVSPEYBWEYHUVRWFUEWGUVSUWBUUKUVPUVQWEWGUWC
      UUKUVTJZUUKUWAJZUHUVOUUKUVTUWAWHUWEUVOUWDUWEUUKCCWIZCUWAUWFUUKCCWJWGCWKWL
      WMVAWNUVOUVLIUVOUVJUVKUVOUVOUUQCJZUUKUUQEUGZUBZUWGUVOUUQUUKEUGZUBZUHZUVKU
      VJUWLUVKUKUVOUWIUWKUVKUWHUVOUWKUVKUKZUWGUWHUUSUWMABUUKUUQEUVEUVDFUSUWKUUS
      UVKUWJUWGUUSUVKUKZUVOUWJUUQUUKPZUWNABUUQUUKEUVDUVEFUSUUSUWOUVKUVKUUSUWOUH
      UUKUUQWOWPWQVAVDVBVAVDVEWRUVHUWIUVIUWKUUQVJJUVHUWIVKUVDUUKUUQCCEVJVLUEUUK
      VJJUVIUWKVKUVEUUQUUKCCEVJVLUEWSWTUVKUVOUVJUVKUVOUVJUKUWGUUQUUQYCUGZUWPUHZ
      UKZUWRUWGUWPUKUWGUWPUWGUWGUUQUUQEUGZUBUWGUWGUWGUWSUWGXAZUWTUWSUWGUWSUUQUU
      QPUUQXBABUUQUUQEUVDUVDFUSXCWRXDUUQUUQCCECVLXEUWGUWPXFXCUVKUVOUWGUVJUWQUUK
      UUQCXGUVKUVHUWPUVIUWPUUKUUQUUQYCULUUKUUQUUQYCUPXHUNXIVBXJXKXLXMXNYAYCVJJZ
      YDYKVKYAYBVJJZUXAYAUXBCCDDXOXPUXBYBELVJYCYBEVJXQEYBXRXSXLVJYCXTXLXI $.
      $( [23-Sep-2008] $) $( [22-Sep-2008] $)
  $}

  ${ $d F x y $. $d G x y $.
    definc.1  $e |- G e. X $.
    definc.2  $e |- F e. Y $.
    definc.3  $e |- C = { <. x , y >. | x (_ y } $.
    $( Note: remove definc. 1 using opelxp1. $)
    $( Definition of the inclusion. $)
    definc $p |- ( G ( C i^i ( A X. B ) ) F <->
      ( G e. A /\ F e. B /\ G (_ F ) ) $=
      ( cxp cin wbr wa wss wcel w3a int2rel elisseti inposetlem brxp anbi12i
      simprl simprr pm3.26 3jca 3simp3 3simpa jca impbii 3bitri ) GFECDMZNOGFEO
      ZGFUNOZPGFQZGCRZFDRZPZPZURUSUQSZGFEUNTUOUQUPUTABGFEGHJUAFIKUAZLUBGFCDVCUC
      UDVAVBVAURUSUQUQURUSUEUQURUSUFUQUTUGUHVBUQUTURUSUQUIURUSUQUJUKULUM $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${ $d C x $. $d x y $.
    dominc.1  $e |- C = { <. x , y >. | x (_ y } $.
    $( The domain of the inclusion relation is ` V ` . $)
    dominc $p |- dom C = V $=
      ( cdm cvv wceq cv wcel eqv wss wex cab ssid weq wb sseq2 equcoms biimpd
      a4imev ax-mp abid mpbir copab dmeqi dmopab eqtri eleqtrri mpgbir ) CEZFGA
      HZUJIAAUJJUKUKBHZKZBLZAMZUJUKUOIUNUKUKKZUNUKNUPUMBABAOUPUMUPUMPABUKULUKQR
      STUAUNAUBUCUJUMABUDZEUOCUQDUEUMABUFUGUHUI $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${ $d C y $. $d x y $.
    rninc.1  $e |- C = { <. x , y >. | x (_ y } $.
    $( The range of the inclusion relation is ` V ` . $)
    rninc $p |- ran C = V $=
      ( crn cvv wceq cv wcel eqv wss wex cab ssid weq wb sseq1 equcoms biimpd
      a4imev ax-mp abid mpbir copab rneqi rnopab eqtri eleqtrri mpgbir ) CEZFGB
      HZUJIBBUJJUKAHZUKKZALZBMZUJUKUOIUNUKUKKZUNUKNUPUMABABOUPUMUPUMPBAUKULUKQR
      STUAUNBUBUCUJUMABUDZEUOCUQDUEUMABUFUGUHUI $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${ $d A x y $.
    domncnt.1  $e |- C = { <. x , y >. | x (_ y } $.
    $( Domain of the intersection of the inclusion with a square
       cross product. $)
    domncnt $p |- dom ( C i^i ( A X. A ) ) = A $=
      ( cxp cin cdm cv wss copab wcel wa df-xp ineq12i dmeqi inopab simprl
      simprr pm3.26 jca impbii opabbii eqtri wex wral wceq wrex ssid sseq2
      rcla4ev mpan2 df-rex sylib rgen dmopab3 mpbi 3eqtri ) DCCFZGZHAIZBIZJZABK
      ZVACLZVBCLZMZABKZGZHVEVFVCMZMZABKZHZCUTVIDVDUSVHEABCCNOPVIVLVIVCVGMZABKVL
      VCVGABQVNVKABVNVKVNVEVJVCVEVFRVNVFVCVCVEVFSVCVGTUAUAVKVCVGVEVFVCSVKVEVFVE
      VJTVEVFVCRUAUAUBUCUDPVJBUEZACUFVMCUGVOACVEVCBCUHZVOVEVAVAJZVPVAUIVCVQBVAC
      VBVAVAUJUKULVCBCUMUNUOVJABCUPUQUR $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${ $d A x y $.
    ranncnt.1  $e |- C = { <. x , y >. | x (_ y } $.
    $( Range of the intersection of the inclusion with a square
       cross product. $)
    ranncnt $p |- ran ( C i^i ( A X. A ) ) = A $=
      ( cxp cin crn cv wss copab wcel wa df-xp ineq12i rneqi incom inopab
      eqtri ccnv cdm df-rn cnvopab dmeqi simplr simpll pm3.27 jca simprl
      pm3.26 simprr impbii opabbii wex wral wceq ssid wrex sseq1 rcla4ev
      ancoms df-rex sylib mpan rgen dmopab3 mpbi 3eqtri ) DCCFZGZHAIZBIZJZABKZV
      KCLZVLCLZMZABKZGZHVQVMMZABKZHZCVJVSDVNVIVREABCCNOPVSWAVSVRVNGWAVNVRQVQVMA
      BRSPWBWATZUAVTBAKZUAZCWAUBWCWDVTABUCUDWEVPVOVMMZMZBAKZUAZCWDWHVTWGBAVTWGV
      TVPWFVOVPVMUEVTVOVMVOVPVMUFVQVMUGUHUHWGVQVMWGVOVPVPVOVMUIVPWFUJUHVPVOVMUK
      UHULUMUDWFAUNZBCUOWICUPWJBCVLVLJZVPWJVLUQWKVPMVMACURZWJVPWKWLVMWKAVLCVKVL
      VLUSUTVAVMACVBVCVDVEWFBACVFVGSVHVH $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  ${ $d A r x y $. $d R r x y $.
  $( Value of an infimum under a weak ordering. $)
  nfwval $p |- ( ( R e. U /\ A e. W ) -> ( R infw A ) = ( `' R supw A ) ) $=
    ( vr vx vy cv ccnv cspw co cinf oprex wceq cnveq opreq1d opreq2 df-nfw
    oprabval5 ) EFGBACDEHZIZFHZJKBIZAJKLUCUBJKUCAJMTBNUAUCUBJTBOPUBAUCJQFGERS
    $.
    $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}

  $( The empty set is a poset. $)
  empos $p |- (/) e. Poset $=
    ( c0 cps wcel wrel ccom wss ccnv cin cid cuni cres wceq w3a rel0 co01
    eqimssi cnv0 ineq2i inidm uni0 unieqi eqtri reseq2 ax-mp res0 eqtr2i
    3eqtri 3pm3.2i cvv wb 0ex isps mpbir ) ABCZADZAAEZAFZAAGZHZIAJZJZKZLZMZUOUQ
    VCNUPAAOPUSAAHAVBURAAQRASVBIAKZAVAALVBVELVAUTAUTATUATUBVAAIUCUDIUEUFUGUHAUI
    CUNVDUJUKUIAULUDUM $.
    $( [7-Oct-2008] $) $( [6-Oct-2008] $)

  $( The converse of a poset is a poset. $)
  dupos1 $p |- ( R e. Poset -> `' R e. Poset ) $=
    ( cps wcel ccnv wrel ccom wss cin cid cuni cres wceq w3a isps wi relcnv
    a1i cnvss cnvco syl5ssr dfrel2 ineq2 eqeq1d imbi2d wa reseq2 eqeq2d biimpa
    incom syl5eq ex syl5bir sylbi psrel relcnvfld syl sylc 3anim123d sylbid
    pm2.43i cvv wb cnvexg mpbird ) ABCZADZBCZVFEZVFVFFZVFGZVFVFDZHZIVFJJZKZLZMZ
    VEVPVEVEAEZAAFZAGZAVFHZIAJJZKZLZMVPBANVEVQVHVSVJWCVOVQVHOVEVHVQAPQQVSVJOVEV
    SVRDVFVIVRARAASTQVQWAVMLZWCVOOZVEVQVKALZWDWEOAUAWFWEWCVFAHZVNLZOWDWFVOWHWCW
    FVLWGVNVKAVFUBUCUDWDWCWHWDWCUEVTVNWGWDWCVTVNLWDWBVNVTWAVMIUFUGUHVFAUIUJUKUL
    UMAUNZVEVQWDWIAUOUPUQURUSUTVEVFVACVGVPVBABVCVAVFNUPVD $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)

  $( A poset is transitive. $)
  postr $p |- ( R e. Poset -> ( R o. R ) (_ R ) $=
    ( cps wcel wrel ccom wss ccnv cin cid cuni cres wceq w3a isps ibi 3simp2d
    ) ABCZADZAAEAFZAAGHIAJJKLZQRSTMBANOP $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)

  $( A poset is antisymmetric and reflexive. $)
  posanref $p |- ( R e. Poset -> ( R i^i `' R ) = ( I |` U. U. R ) ) $=
    ( cps wcel wrel ccom wss ccnv cin cid cuni cres wceq w3a isps ibi 3simp3d
    ) ABCZADZAAEAFZAAGHIAJJKLZQRSTMBANOP $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)


  $( The converse of a poset is a poset. $)
  dupos2 $p |- ( Rel R -> ( R e. Poset <-> `' R e. Poset ) ) $=
    ( wrel cps wcel ccnv dupos1 wceq wi dfrel2 eleq1 biimpd sylbi syl5 impbid2
    ) ABZACDZAEZCDZAFOQEZCDZPROSAGZTPHAIUATPSACJKLQFMN $.
    $( [28-Jan-2009] $) $( [5-Jan-2009] $)

  ${ $d x y z $.
    $( The "less than or equal to" relationship on the extended reals
     is a poset. $)
    lteqtpos  $p |- <_ e. Poset $=
      ( vx vy vz cle cvv wcel cps cxr cxp clt ccnv cdif df-le xrex xpex difexg
      ax-mp eqeltri wrel ccom wss cin cid cuni cres wceq w3a leqrl cv wbr wa
      wi wal visset xrletr2 ax-gen gen2 cotr mpbir weq wb asymref fldleqt
      eleq2i mlteqer letri31 biimprd exp3a com23 ancoms syl6 com4l pm2.43i
      impcom com12 eleq1 biimpd ex com3l syl6bi pm2.43a com13 impbid 19.21aiv
      sylbi mprgbir 3pm3.2i isps mpbiri ) DEFZDGFZDHHIZJKZLZEMWLEFWNEFHHNNOWLWM
      EPQRWJWKDSZDDTDUAZDDKUBUCDUDUDZUEUFZUGWOWPWRUHWPAUIZBUIZDUJZWTCUIZDUJUKWS
      XBDUJULZCUMZBUMAUMXDABXCCXBEFXCCUNWSWTXBEUOQUPUQABCDURUSWRXAWTWSDUJZUKZAB
      UTZVAZBUMZAWQABDVBWSWQFWSHFZXIWQHWSVCVDXJXHBXJXFXGXFXJXGXEXAXJXGULZXEXAXK
      ULXJXEXEXAXGXJXEWTHFZXJUKXEXAXGULULZWTWSHVEXJXLXMXJXLUKZXAXEXGXNXAXEXGXNX
      GXFWSWTVFZVGVHVIVJVKVLVMVNVOXJXGXFULZXGXJXJXFXJXGXJXFULZXGXJXLXGXQULWSWTH
      VPXJXLXGXFXJXLXPXNXGXFXOVQVRVSVTWAWBVMWCWDWEWFWGEDWHWIQ $.
      $( [3-Aug-2009] $) $( [2-Aug-2009] $)
  $}

  $( A restriction of the identity is a poset. $)
  dispos $p |- ( A e. B -> ( I |` A ) e. Poset ) $=
    ( wcel cid cres cps wrel ccom wss ccnv cin cuni wceq w3a relres wf wf1o
    f1oi f1of ax-mp fcoi1 eqimssi cnvresid ineq2i inidm cdm crn cun relfld
    dmresi rnresi uneq12i unidm 3eqtrri reseq2 3eqtri 3pm3.2i cvv wb resiexg
    isps syl mpbiri ) ABCZDAEZFCZVEGZVEVEHZVEIZVEVEJZKZDVELLZEZMZNZVGVIVNDAOZVH
    VEAAVEPZVHVEMAAVEQVQARAAVESTAAVEUATUBVKVEVEKVEVMVJVEVEAUCUDVEUEAVLMVEVMMVLV
    EUFZVEUGZUHZAAUHAVGVLVTMVPVEUITVRAVSAAUJAUKULAUMUNAVLDUOTUPUQVDVEURCVFVOUSA
    BUTURVEVAVBVC $.
    $( [3-Aug-2009] $) $( [2-Aug-2009] $)


  ${ $d A x y $. $d R x y $. $d R x z $.
    $( Any subset of a partially ordered set is partially ordered. $)
    pospos $p |- ( R e. Poset -> ( R i^i ( A X. A ) ) e. Poset )  $=
      ( vx vy vz cps wcel cxp cin wrel ccom wss ccnv cid cuni cres wceq w3a
      isps relin1 tricptr cv wbr wral wa weq wi wal asymref2 df-ral uuniin
      ssel elin visset brxp pm2.27 int2rel biimpri ex syl6 com3r sylbir
      fldsqcp2 eleq2i sylancb impcom sylbi ax-mp com12 a4s adantr r19.21aiv
      expcom imp 19.20i2 adantl jca sylibr 3anim123i syl6bi pm2.43i cvv wb
      inex1g syl mpbird ) BFGZBAAHZIZFGZWIJZWIWIKWILZWIWIMINWIOOZPQZRZWGWOWGWGB
      JZBBKBLZBBMINBOOZPQZRWOFBSWPWKWQWLWSWNBWHTABUAWSCUBZWTWIUCZCWMUDZWTDUBZWI
      UCZXCWTWIUCZUEZCDUFZUGZDUHCUHZUEWNWSXBXIWSXACWMWSWTWTBUCZCWRUDZWTEUBZBUCX
      LWTBUCUECEUFUGEUHCUHZUEWTWMGZXAUGZCEBUIXKXOXMXKWTWRGZXJUGZCUHXOXJCWRUJXQX
      OCXNXQXAWMWRWHOOZIZLZXNXQXAUGZUGBWHUKXTXNWTXSGZYAWMXSWTULYBXPWTXRGZUEYAWT
      WRXRUMYCXPYAWTAGZYDXPYAUGZYCYDYDUEWTWTWHUCZYEWTWTAACUNUOXPXQYFXAXPXQXJYFX
      AUGXPXJUPXJYFXAXAXJYFUEWTWTBWHUQURUSUTVAVBXRAWTAVCVDZYGVEVFVGUTVHVIVJVGVK
      VGVLWSXKWTXCBUCZXCWTBUCZUEZXGUGZDUHCUHZUEXICDBUIYLXIXKYKXHCDXFYKXGXDXEYKX
      GUGZXDYHWTXCWHUCZUEXEYMUGZWTXCBWHUQYHYOYNXEYHYMXEYIXCWTWHUCZUEYHYMUGZXCWT
      BWHUQYIYQYPYHYIYMYJXGUPVMVKVGVIVKVGVNVIVOVPVGVQCDWIUIVRVSVTWAWGWIWBGWJWOW
      CBWHFWDWBWISWEWF $.
      $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}

  ${ $d A x y $. $d R x y $.
  $( Any subset of a totally ordered set is totally ordered. $)
  tostos $p |- ( R e. Toset -> ( R i^i ( A X. A ) ) e. Toset )  $=
    ( vx vy ccha wcel cxp cin cps cv wbr wo cuni wral wa eqid istoset biimpd
    pospos wi wss uuniin ssel2 elin w3a ax-17 hbra1 hbim hb3an pm2.27 ra4 a1d
    syl6 3imp sseli pm3.26bi syl syl5 imp fldsqcp2 eleq2i biimpi 3ad2ant3
    adantl sylbi anim12i visset brxp sylibr jca andir sylib sqpeq ersymb
    anbi2i orbi2i int2rel orbi12i ex r19.21ai 3exp com23 mpan com12 r19.20i2
    adantr cvv wb inex1g ad2antrr mpbird syli pm2.43i ) BEFZBAAGZHZEFZWNWNBIFZC
    JZDJZBKZWTWSBKZLZDBMMZNZCXDNZOZWQWNWNXGCDEBXDXDPQRXGWNWQXGWNOZWQWPIFZWSWTWP
    KZWTWSWPKZLZDWPMMZNZCXMNZOZXGXPWNWRXIXFXOABSXEXNCXDXMWSXMFZWSXDFZXETZXNXMXD
    WOMMZHZUAZXQXSXNTZBWOUBZYBXQOWSYAFZYCXMYAWSUCYEXRWSXTFZOYCWSXDXTUDXRYFYCXRX
    SYFXNXRXSYFXNXRXSYFUEZXLDXMXRXSYFDXRDUFZXRXEDYHXCDXDUGUHYFDUFUIYGWTXMFZXLYG
    YIOZXAWSWTWOKZOZXBWTWSWOKZOZLZXLYJYLXBYKOZLZYOYJXCYKOYQYJXCYKYGYIXCYGWTXDFZ
    XCYIXRXSYFYRXCTZXRXSXEYFYSTXRXEUJXEYSYFXCDXDUKULUMUNYIWTYAFZYRXMYAWTYDUOZYT
    YRWTXTFZWTXDXTUDZUPUQURUSYJWSAFZWTAFZOYKYGUUDYIUUEYFXRUUDXSYFUUDXTAWSAUTZVA
    VBVCYIYTUUEUUAYTYRUUBOUUEUUCUUBUUEYRUUBUUEXTAWTUUFVAVBVDVEUQVFWSWTAADVGZVHV
    IVJXAXBYKVKVLYPYNYLYKYMXBWSWTWOCVGUUGAVMVNVOVPVLXJYLXKYNWSWTBWOVQWTWSBWOVQV
    RVIVSVTWAWBUSVEUQWCWDWEVFWFXHWPWGFZWQXPWHWRUUHXFWNBWOIWIWJCDWGWPXMXMPQUQWKV
    SWLWM $.
    $( [24-Jan-2010] $) $( [24-Jan-2010] $)
  $}


  ${ $d C a u v x y $. $d J a u v x y $.
    toplat.1  $e |- C = { <. u , v >. | u (_ v } $.
    $( A topology is a lattice. This fact leads to the idea of pointless
       topology. $)
    toplat $p |- ( J e. Top -> ( C i^i ( J X. J ) ) e. Lat ) $=
      ( vx vy va ctop wcel cxp cin cps cv cpr cspw co cinf wa wral cla inposet
      cuni cvv wbr wi wceq cdm domncnt eqcomi spwpr4 adantr zfpair uniex jctir
      wss w3a simprl uniopn visset prss sylan2b prid1 a1i ssid jctil ssuni syl
      3jca definc sylibr simprr prid2 jca cun unss ad2antrl unipr sseq1i
      biimpi ex sylbi expcom 3ad2ant3 com12 imp r19.21aiva syl3anc eqeltrd
      ccnv cint nfwval crn ranncnt df-rn eqtr3i dupos1 c0 wne prnz intex mpbi
      intpr eleq1i adantl pm3.26 inss1 inex1 syl5eqbr brcnvg mpbird adantlr
      pm3.27 inss2 eqsstri adantll inopn 3expb syl5eqel sylanc simplr ad2antrr
      ssin syl6ssr syl2ani wb sylan biimpd syl2and pm3.2i exp31 r19.21aiv
      3eqtrd r19.21aivv isla ) DIJZCDDKLZMJZYQFNZGNZOZPQZDJZYQUUARQZDJZSZGDTFDT
      ZSYQUAJYPYRUUGBADICEUBZYPUUFFGDDYPYSDJZYTDJZSZUUFYPUUKSZUUCUUEUULUUBUUAUC
      ZDYRUUMUDJZSYSUUMYQUEZYTUUMYQUEZSYSHNZYQUEZYTUUQYQUEZSZUUMUUQYQUEZUFZHDTU
      UBUUMUGUULHYSYTUUMUDYQDYQUHDBADCEUIUJZUKUULYRUUNYPYRUUKUUHULZUUAFGUMZUNZU
      OUULUUOUUPUULUUIUUMDJZYSUUMUPZUQUUOUULUUIUVGUVHYPUUIUUJURZYPUUADUPUVGUUKU
      UADUSYSYTDFUTZGUTZVAVBZUULYSYSUPZYSUUAJZSUVHUULUVNUVMUVNUULYSYTUVJVCVDYSV
      EVFYSYSUUAVGVHVIBADDCUUMYSUDUDUVJUVFEVJVKUULUUJUVGYTUUMUPZUQUUPUULUUJUVGU
      VOYPUUIUUJVLZUVLUULYTYTUPZYTUUAJZSUVOUULUVRUVQUVRUULYSYTUVKVMVDYTVEVFYTYT
      UUAVGVHVIBADDCUUMYTUDUDUVKUVFEVJVKVNUULUVBHDUUTUULUUQDJZSZUVAUURUUSUVTUVA
      UFZUURUUIUVSYSUUQUPZUQUUSUWAUFZBADDCUUQYSUDUDUVJHUTZEVJUWBUUIUWCUVSUUSUWB
      UWAUUSUUJUVSYTUUQUPZUQUWBUWAUFZBADDCUUQYTUDUDUVKUWDEVJUWEUUJUWFUVSUWBUWEU
      WAUWBUWESYSYTVOZUUQUPZUWAYSYTUUQVPUWHUVTUVAUWHUVTSZUVGUVSUUMUUQUPZUQUVAUW
      IUVGUVSUWJUULUVGUWHUVSUVLVQUWHUULUVSVLUWHUWJUVTUWHUWJUWGUUMUUQUUMUWGYSYTU
      VJUVKVRUJVSVTULVIBADDCUUQUUMUDUDUVFUWDEVJVKWAWBWCWDWBWEWDWBWFWEWGWHUVLWIU
      ULUUDYSYTLZDUULUUDYQWJZUUAPQZUUAWKZUWKUULYRUUAUDJZSUUDUWMUGUULYRUWOUVDUVE
      UOUUAYQMUDWLVHUWLMJZUWNUDJZSYSUWNUWLUEZYTUWNUWLUEZSZYSUUQUWLUEZYTUUQUWLUE
      ZSZUWNUUQUWLUEZUFZHDTUWMUWNUGUULHYSYTUWNUDUWLDYQWMDUWLUHBADCEWNYQWOWPUKUU
      LUWPUWQYPUWPUUKYPYRUWPUUHYQWQVHULUUAWRWSUWQYSYTUVJWTUUAXAXBZUOUUKUWNDJZUW
      TUULUUKUXGSUWRUWSUUIUXGUWRUUJUUIUXGSZUWRUWNYSYQUEUXHUWKYSUWNYQUXHUWKDJZUU
      IUWKYSUPZUQUWKYSYQUEUXHUXIUUIUXJUXGUXIUUIUXGUXIUWNUWKDYSYTUVJUVKXCZXDVTXE
      UUIUXGXFUXJUXHYSYTXGVDVIBADDCYSUWKUDUDYSYTUVJXHUVJEVJVKUXKXIYSUWNDDYQXJXK
      XLUUJUXGUWSUUIUUJUXGSZUWSUWNYTYQUEZUXLUXGUUJUWNYTUPZUQUXMUXLUXGUUJUXNUUJU
      XGXMUUJUXGXFUXNUXLUWNUWKYTUXKYSYTXNXOVDVIBADDCYTUWNUDUDUXFUVKEVJVKYTUWNDD
      YQXJXKXPVNYPUUKXMUULUWKDUWNYPUUIUUJUXIYSYTDXQXRZUXKXSZXTUULUXEHDUULUVSUXC
      UXDUVTUXCSZUXDUUQUWNYQUEZUVTUXCUXRUVTUUQYSYQUEZUUQYTYQUEZUXRUXAUXBUVTUVSU
      UIUUQYSUPZUQZUVSUUJUUQYTUPZUQZUXRUXSUXTUVTUYBUYDSZUXRUVTUYESZUVSUXGUUQUWN
      UPZUQUXRUYFUVSUXGUYGUULUVSUYEYAUULUXGUVSUYEUXPYBUYEUYGUVTUYBUYDUYGUYAUVSU
      YDUYGUFUUIUYDUYAUYGUYCUVSUYAUYGUFUUJUYAUYCUYGUYAUYCSZUUQUWKUWNUYHUUQUWKUP
      UUQYSYTYCVTUXKYDWCWDWEWDWFXEVIBADDCUWNUUQUDUDUWDUXFEVJVKWAUXSUYBBADDCYSUU
      QUDUDUWDUVJEVJVTUXTUYDBADDCYTUUQUDUDUWDUVKEVJVTYEUVTUXAUXSUUIUVSUXAUXSYFU
      ULYSUUQDDYQXJUVIYGYHUVTUXBUXTUUJUVSUXBUXTYFUULYTUUQDDYQXJUVPYGYHYIWFUXQUW
      QUUQUDJZSZUXDUXRYFUYJUXQUWQUYIUXFUWDYJVDUWNUUQUDUDYQXJVHXKYKYLWHUWNUWKUGU
      ULUXKVDYMUXOWIVNWAYNVNFGYQDUVCYOVK $.
      $( [7-Sep-2009] $) $( [6-Sep-2009] $)
  $}


