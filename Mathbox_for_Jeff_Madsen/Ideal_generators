$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ideal generators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c IdlGen $.

  $( Extend class notation with the ideal generation function. $)
  cigen $a class IdlGen $.

  ${
    $d r s i j $.
    $( Define the ideal generated by a subset of a ring. $)
    df-igen $a |- IdlGen = ( r e. RingOps , s e. ~P ran ( 1st ` r ) |->
                      |^| { j e. ( Idl ` r ) | s C_ j } ) $.
  $}

  ${
    $d R r s i j $.  $d S r s i j $.  $d X r s i j $.  $d I j $.
    igenval.1 $e |- G = ( 1st ` R ) $.
    igenval.2 $e |- X = ran G $.
    $( The ideal generated by a subset of a ring.  (Contributed by Jeff Madsen,
       10-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       20-Dec-2013.) $)
    igenval $p |- ( ( R e. RingOps /\ S C_ X ) -> ( R IdlGen S ) =
                                        |^| { j e. ( Idl ` R ) | S C_ j } ) $=
      ( vr vs cring wcel wss cv cidl cfv crab cint cvv cigen co wceq wa c0 wne
      wrex rngidl sseq2 rcla4ev sylan rabn0 sylibr intex sylib cpw crn c1st
      fvex eqeltri rnex elpw2 simpl fveq2d wb sseq1 adantl rabeqbidv inteqd
      fveq2 syl6eqr rneqd pweqd df-igen ovmpt2x syl3an2br mpd3an3 ) AJKZBELZBCM
      ZLZCANOZPZQZRKZABSTWBUAZVPVQUBZWAUCUDZWCWEVSCVTUEZWFVPEVTKVQWGADEFGUFVSVQ
      CEVTVREBUGUHUIVSCVTUJUKWAULUMVQVPBEUNZKWCWDBEEDUOZRGDDAUPOZRFAUPUQURUSURU
      THIABJHMZUPOZUOZUNIMZVRLZCWKNOZPZQWBSRWHWKAUAZWNBUAZUBZWQWAWTWOVSCWPVTWTW
      KANWRWSVAVBWSWOVSVCWRWNBVRVDVEVFVGWRWMEWRWMWIEWRWLDWRWLWJDWKAUPVHFVIVJGVI
      VKCIHVLVMVNVO $.
      $( [20-Dec-2013] $) $( [10-Jun-2010] $)

    $( A set is a subset of the ideal it generates.  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    igenss $p |- ( ( R e. RingOps /\ S C_ X ) -> S C_ ( R IdlGen S ) ) $=
      ( vj cring wcel wss wa cigen co cv cidl cfv crab cint ssintub igenval
      sseq2d mpbiri ) AHIBDJKZBABLMZJBBGNJGAOPZQRZJGBUESUCUDUFBABGCDEFTUAUB $.
      $( [10-Jun-2010] $)

    $( The ideal generated by a set is an ideal.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    igenidl $p |- ( ( R e. RingOps /\ S C_ X ) ->
                              ( R IdlGen S ) e. ( Idl ` R ) ) $=
      ( vj cring wcel wss wa cigen co cv cidl cfv crab cint igenval c0 wne
      wrex rngidl sseq2 rcla4ev sylan rabn0 sylibr ssrab2 intidl mp3an3 syldan
      eqeltrd ) AHIZBDJZKZABLMBGNZJZGAOPZQZRZUSABGCDEFSUNUOUTTUAZVAUSIZUPURGUSU
      BZVBUNDUSIUOVDACDEFUCURUOGDUSUQDBUDUEUFURGUSUGUHUNVBUTUSJVCURGUSUIUTAUJUK
      ULUM $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R j $.  $d S j $.  $d I j $.
    $( The ideal generated by a set is the minimal ideal containing that set.
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    igenmin $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) /\ S C_ I )
                                              -> ( R IdlGen S ) C_ I ) $=
      ( vj cring wcel cidl cfv wss w3a cigen co cv crab cint wceq c1st crn
      eqid idlss wa sstr ancoms igenval sylan2 anassrs syldanl 3impa sseq2
      elrab intss1 sylbir 3adant1 eqsstrd ) AEFZCAGHZFZBCIZJABKLZBDMZIZDUPNZOZC
      UOUQURUSVCPZUOUQCAQHZRZIZURVDAVECVFVESZVFSZTUOVGURVDVGURUAUOBVFIZVDURVGVJ
      BCVFUBUCABDVEVFVHVIUDUEUFUGUHUQURVCCIZUOUQURUACVBFVKVAURDCUPUTCBUIUJCVBUK
      ULUMUN $.
      $( [10-Jun-2010] $)

    $( The ideal generated by an ideal is that ideal.  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    igenidl2 $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) )
                                          -> ( R IdlGen I ) = I ) $=
      ( vj cring wcel cidl cfv wa cigen co cv wss crab cint c1st crn wceq eqid
      idlss igenval syldan intmin adantl eqtrd ) ADEZBAFGZEZHABIJZBCKLCUFMNZBUE
      UGBAOGZPZLUHUIQAUJBUKUJRZUKRZSABCUJUKULUMTUAUGUIBQUECBUFUBUCUD $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R i j $.  $d S i j $.  $d I j $.  $d X i $.
    igenval2.1 $e |- G = ( 1st ` R ) $.
    igenval2.2 $e |- X = ran G $.
    $( The ideal generated by a subset of a ring.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    igenval2 $p |- ( ( R e. RingOps /\ S C_ X ) -> ( ( R IdlGen S ) = I <->
                                  ( I e. ( Idl ` R ) /\ S C_ I /\
                          A. j e. ( Idl ` R ) ( S C_ j -> I C_ j ) ) ) ) $=
      ( vi cring wcel wss wa cigen co wceq cidl cfv cv wi wral w3a igenidl
      igenss igenmin 3expia r19.21aiva adantr 3jca eleq1 sseq2 sseq1 imbi2d
      ralbidv 3anbi123d syl5ibcom 3adant3r3 adantlr crab cint ralrab biimpri
      ssint sylibr 3ad2ant3 adantl igenval sseqtr4d eqssd ex impbid ) AJKZBFLZM
      ZABNOZEPZEAQRZKZBELZBCSZLZEVTLZTZCVQUAZUBZVNVOVQKZBVOLZWAVOVTLZTZCVQUAZUB
      VPWEVNWFWGWJABDFGHUCABDFGHUDVLWJVMVLWICVQVLVTVQKWAWHABVTUEUFUGUHUIVPWFVRW
      GVSWJWDVOEVQUJVOEBUKVPWIWCCVQVPWHWBWAVOEVTULUMUNUOUPVNWEVPVNWEMZVOEVLWEVO
      ELZVMVLVRVSWLWDABEUEUQURWKEBISZLZIVQUSZUTZVOWEEWPLZVNWDVRWQVSWDWBCWOUAZWQ
      WRWDWNWAWBCIVQWMVTBUKVAVBCEWOVCVDVEVFVNVOWPPWEABIDFGHVGUHVHVIVJVK $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R j x y u v w r s $.  $d X j x y u v w r s $.  $d G x y r s $.
    $d H j x y u v w r s $.  $d A j x y u v w r s $.
    prnc.1 $e |- G = ( 1st ` R ) $.
    prnc.2 $e |- H = ( 2nd ` R ) $.
    prnc.3 $e |- X = ran G $.
    $( A principal ideal (an ideal generated by one element) in a commutative
       ring.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    prnc $p |- ( ( R e. CRing /\ A e. X ) -> ( R IdlGen { A } ) =
                                  { x e. X | E. y e. X x = ( y H A ) } ) $=
      ( vj vu vv vw vr vs ccring wcel wa csn cigen co cv wceq wrex crab cidl
      cfv wss wi wral w3a cgi cring crngrng ssrab2 a1i eqid ring0cl adantr
      ringolz eqcomd opreq1 eqeq2d rcla4ev syl11anc eqeq1 rexbidv elrab
      sylanbrc cbvrexv syl6bb ringodir 3exp2 imp42 ringgcl 3expib imdistani
      ringocl 3expa mpan2 ad2antlr sylan eqeltrrd an32s anassrs opreq2 eleq1d
      syl5ibrcom r19.23adva adantld syl5bi r19.21aiv ringoass anass1rs
      r19.21aiva jca ralbidv anbi12d 3jca wb isidlc mpbird simpr crn c1st
      rneqi eqtri ring1cl ringolidm snssd snssg biimpar idllmulcl eleq1 rabss
      sylibr ex syl5 expdimp snssi igenval2 syl2an ) DQRZCGRZSZDCTZUAUBAUCZBUCZ
      CFUBZUDZBGUEZAGUFZUDZYMDUGUHZRZYGYMUIZYGKUCZUIZYMYRUIZUJZKYOUKZULZYFYPYQU
      UBYFYPYMGUIZEUMUHZYMRZLUCZMUCZEUBZYMRZMYMUKZNUCZUUGFUBZYMRZNGUKZSZLYMUKZU
      LZYDDUNRZYEUURDUOZUUSYESZUUDUUFUUQUUDUVAYLAGUPUQUVAUUEGRZUUEYJUDZBGUEZUUF
      UUSUVBYEDEGUUEHJUUEURZUSUTZUVAUVBUUEUUECFUBZUDZUVDUVFUVAUVGUUECDEFGUUEUVE
      JHIVAVBUVCUVHBUUEGYIUUEUDYJUVGUUEYIUUECFVCVDVEVFYLUVDAUUEGYHUUEUDYKUVCBGY
      HUUEYJVGVHVIVJUVAUUPLYMUUGYMRUUGGRZUUGOUCZCFUBZUDZOGUEZSUVAUUPYLUVMAUUGGY
      HUUGUDZYLUUGYJUDZBGUEUVMUVNYKUVOBGYHUUGYJVGVHUVOUVLBOGYIUVJUDYJUVKUUGYIUV
      JCFVCVDVKVLVIUVAUVMUUPUVIUVAUVLUUPOGUVAUVJGRZSZUUPUVLUVKUUHEUBZYMRZMYMUKZ
      UULUVKFUBZYMRZNGUKZSUVQUVTUWCUVQUVSMYMUUHYMRUUHGRZUUHPUCZCFUBZUDZPGUEZSUV
      QUVSYLUWHAUUHGYHUUHUDZYLUUHYJUDZBGUEUWHUWIYKUWJBGYHUUHYJVGVHUWJUWGBPGYIUW
      EUDYJUWFUUHYIUWECFVCVDVKVLVIUVQUWHUVSUWDUVQUWGUVSPGUVQUWEGRZSUVSUWGUVKUWF
      EUBZYMRZUVAUVPUWKUWMUUSUVPUWKSZYEUWMUUSUWNSZYESUVJUWEEUBZCFUBZUWLYMUUSUVP
      UWKYEUWQUWLUDZUUSUVPUWKYEUWRUVJUWECDEFGHIJVMVNVOUWOUUSUWPGRZSZYEUWQYMRZUU
      SUWNUWSUUSUVPUWKUWSUVJUWEDEGHJVPVQVRUWTYESUWQGRZUWQYJUDZBGUEZUXAUUSUWSYEU
      XBUWPCDEFGHIJVSVTUWSUXDUUSYEUWSUWQUWQUDZUXDUWQURUXCUXEBUWPGYIUWPUDYJUWQUW
      QYIUWPCFVCVDVEWAWBYLUXDAUWQGYHUWQUDYKUXCBGYHUWQYJVGVHVIVJWCWDWEWFUWGUVRUW
      LYMUUHUWFUVKEWGWHWIWJWKWLWMUVQUWBNGUVAUULGRZUVPUWBUVAUXFUVPSZSUULUVJFUBZC
      FUBZUWAYMUUSUXGYEUXIUWAUDZUUSUXFUVPYEUXJUUSUXFUVPYEUXJUULUVJCDEFGHIJWNVNV
      OWEUUSUXGYEUXIYMRZUUSUXGSUUSUXHGRZSZYEUXKUUSUXGUXLUUSUXFUVPUXLUULUVJDEFGH
      IJVSVQVRUXMYESUXIGRZUXIYJUDZBGUEZUXKUUSUXLYEUXNUXHCDEFGHIJVSVTUXLUXPUUSYE
      UXLUXIUXIUDZUXPUXIURUXOUXQBUXHGYIUXHUDYJUXIUXIYIUXHCFVCVDVEWAWBYLUXPAUXIG
      YHUXIUDYKUXOBGYHUXIYJVGVHVIVJWCWEWDWOWPWQUVLUUKUVTUUOUWCUVLUUJUVSMYMUVLUU
      IUVRYMUUGUVKUUHEVCWHWRUVLUUNUWBNGUVLUUMUWAYMUUGUVKUULFWGWHWRWSWIWJWKWLWMW
      TWCYDYPUURXAYELMNDEFYMGUUEHIJUVEXBUTXCYFCYMYFYECYJUDZBGUEZCYMRYDYEXDYDUUS
      YEUXSUUTUVAFUMUHZGRZCUXTCFUBZUDZUXSUUSUYAYEDUXTFGGEXEDXFUHZXEJEUYDHXGXHZI
      UXTURZXIUTUVAUYBCCDUXTFGIUYEUYFXJVBUXRUYCBUXTGYIUXTUDYJUYBCYIUXTCFVCVDVEV
      FWCYLUXSACGYHCUDYKUXRBGYHCYJVGVHVIVJXKYDUUSYEUUBUUTUVAUUAKYOUUSYRYORZYEUU
      AUUSUYGSZYEYSYTYEYSSCYRRZUYHYTYEUYIYSCYRGXLXMUYHUYIYTUYHUYISZYLYHYRRZUJZA
      GUKYTUYJUYLAGUYJUYLYHGRUYJYKUYKBGUYJYIGRZSUYKYKYJYRRZUYHUYIUYMUYNCYIDEFYR
      GHIJXNWFYHYJYRXOWIWJUTWPYLAGYRXPXQXRXSXTWEWPWCWTYDUUSYGGUIYNUUCXAYEUUTCGY
      ADYGKEYMGHJYBYCXC $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d K x y z $.  $d X x y z $.  $d G y z $.  $d H x y z $.  $d U x y z $.
    $d Z x y $.
    isfldidl.1 $e |- G = ( 1st ` K ) $.
    isfldidl.2 $e |- H = ( 2nd ` K ) $.
    isfldidl.3 $e |- X = ran G $.
    isfldidl.4 $e |- Z = ( Id ` G ) $.
    isfldidl.5 $e |- U = ( Id ` H ) $.
    $( Determine if a ring is a field based on its ideals.  (Contributed by
       Jeff Madsen, 10-Jun-2010.) $)
    isfldidl $p |- ( K e. Fld <-> ( K e. CRing /\
                                U =/= Z /\ ( Idl ` K ) = { { Z } , X } ) ) $=
      ( vy vx vz cfld wcel ccring wne cidl cfv csn cpr wceq w3a fldcrng cdrng
      flddivrng dvrunz syl divrngidl 3jca cring cv co wrex cdif wral wa
      crngrng 3ad2ant1 simp2 crab crn c1st rneqi eqtri ring1cl ad2antrr cigen
      wn eldif wss snssi igenss sylan2 visset snss biimpri eleq2 syl5ibcom
      con3d imp sylan anasss sylan2b adantlr wo eldifi snssd igenidl an32s
      oprex elpr sylib ord mpd sylanl1 prnc eqtr3d eleqtrd eqeq1 rexbidv elrab
      simprd eqcom rexbii sylibr r19.21aiva 3adant2 jca32 isdivrng3 simp1
      isfld2 sylanbrc impbii ) DOPZDQPZAFRZDSTZFUAZEUBZUCZUDZXPXQXRYBDUEXPDUFPZ
      XRDUGZDABCEFGHIJKUHUIXPYDYBYEDBCEFGHIJUJUIUKYCYDXQXPYCDULPZXRLUMMUMZCUNZA
      UCZLEUOZMEXTUPZUQZURURYDYCYFXRYLXQXRYFYBDUSZUTXQXRYBVAXQYBYLXRXQYBURZYJMY
      KYNYGYKPZURZAYHUCZLEUOZYJYPAEPZYRYPANUMZYHUCZLEUOZNEVBZPYSYRURYPAEUUCXQYS
      YBYOXQYFYSYMDACEEBVCDVDTZVCIBUUDGVEVFHKVGUIVHYPDYGUAZVIUNZEUUCXQYFYBYOUUF
      EUCZYMYFYBURYOURZUUFXTUCZVJZUUGYFYOUUJYBYOYFYGEPZYGXTPZVJZURUUJYGEXTVKYFU
      UKUUMUUJYFUUKURUUEUUFVLZUUMUUJUUKYFUUEEVLZUUNYGEVMDUUEBEGIVNVOUUNUUMUUJUU
      NUUIUULUUNYGUUFPZUUIUULUUPUUNYGUUFMVPVQVRUUFXTYGVSVTWAWBWCWDWEWFUUHUUIUUG
      UUHUUFYAPZUUIUUGWGYFYOYBUUQYFYOURZYBUUQUURUUFXSPZYBUUQYOYFUUOUUSYOYGEYGEX
      TWHZWIDUUEBEGIWJVOXSYAUUFVSVTWBWKUUFXTEDUUEVIWLWMWNWOWPWQXQYOUUFUUCUCZYBY
      OXQUUKUVAUUTNLYGDBCEGHIWRVOWFWSWTUUBYRNAEYTAUCUUAYQLEYTAYHXAXBXCWNXDYIYQL
      EYHAXEXFXGXHXIXJMLDABCEFGHJIKXKXGXQXRYBXLDXMXNXO $.
      $( [10-Jun-2010] $)
  $}

  ${
    isfldidl2.1 $e |- G = ( 1st ` K ) $.
    isfldidl2.2 $e |- H = ( 2nd ` K ) $.
    isfldidl2.3 $e |- X = ran G $.
    isfldidl2.4 $e |- Z = ( Id ` G ) $.
    $( Determine if a ring is a field based on its ideals.  (Contributed by
       Jeff Madsen, 6-Jan-2011.) $)
    isfldidl2 $p |- ( K e. Fld <-> ( K e. CRing /\
                              X =/= { Z } /\ ( Idl ` K ) = { { Z } , X } ) ) $=
      ( cfld wcel ccring cgi cfv wne cidl csn cpr wceq w3a eqid isfldidl wa
      cring wb crngrng eqcom 0ring syl5bb syl necon3bid anbi1d pm5.32i 3anass
      3bitr4i bitri ) CJKCLKZBMNZEOZCPNEQZDRSZTZUQDUTOZVATZURABCDEFGHIURUAZUBUQ
      USVAUCZUCUQVCVAUCZUCVBVDUQVFVGUQUSVCVAUQUREDUTUQCUDKZURESZDUTSZUECUFVIEUR
      SVHVJUREUGCURABDEFGHIVEUHUIUJUKULUMUQUSVAUNUQVCVAUNUOUP $.
      $( [6-Jan-2011] $)
  $}

  ${
    $d R a b x y r s $.  $d P a b x y r s $.  $d X a b x y r s $.
    $d G x y r s $.  $d H a b x y r s $.
    ispridlc.1 $e |- G = ( 1st ` R ) $.
    ispridlc.2 $e |- H = ( 2nd ` R ) $.
    ispridlc.3 $e |- X = ran G $.
    $( The predicate "is a prime ideal".  Alternate definition for commutative
       rings.  (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    ispridlc $p |- ( R e. CRing -> ( P e. ( PrIdl ` R ) <-> ( P e. ( Idl ` R )
                              /\ P =/= X /\ A. a e. X A. b e. X
                      ( ( a H b ) e. P -> ( a e. P \/ b e. P ) ) ) ) ) $=
      ( vx vy vs vr ccring wcel cpridl cfv cidl wne cv co wo wi wral w3a wss
      cring wb crngrng ispridl syl wa csn cigen snssi igenidl syl2an adantrr
      adantrl wceq raleq sseq1 orbi1d imbi12d ralbidv orbi2d rcla42v syl11anc
      adantlr wrex crab cab prnc df-rab syl6eq abeq2d anbi12d adantr reeanv
      anbi2i an4 bitri crngm4 3com23 3expa adantllr ringocl syl3an1 3expb
      idllmulcl sylanl1 anassrs syldan eqeltrrd opreq12 eleq1d syl5ibrcom ex
      r19.23advv adantld syl5bir sylbid r19.21aivv igenss visset snss sylibr
      ssel syl5com orim12d imim12d syld r19.21advva adantrd imdistand df-3an
      3imtr4g ispridl2 impbid ) BOPZABQRPZABSRZPZAETZFUAZGUAZDUBZAPZYFAPZYGAPZU
      CZUDZGEUEFEUEZUFZYAYBYDYEKUAZLUAZDUBZAPZLMUAZUEZKNUAZUEZUUBAUGZYTAUGZUCZU
      DZMYCUENYCUEZUFZYOYABUHPZYBUUIUIBUJZKLABCDENMHIJUKULYAYDYEUMZUUHUMUULYNUM
      UUIYOYAUULUUHYNYAYDUUHYNUDZYEYAYDUUMYAYDUMZUUHYMFGEEUUNYFEPZYGEPZUMZUMZUU
      HYSLBYGUNZUOUBZUEZKBYFUNZUOUBZUEZUVCAUGZUUTAUGZUCZUDZYMYAUUQUUHUVHUDZYDYA
      UUQUMZUVCYCPZUUTYCPZUVIYAUUOUVKUUPYAUUJUVBEUGZUVKUUOUUKYFEUPZBUVBCEHJUQUR
      USYAUUPUVLUUOYAUUJUUSEUGZUVLUUPUUKYGEUPZBUUSCEHJUQURUTUUGUVHUUAKUVCUEZUVE
      UUEUCZUDNMUVCUUTYCYCUUBUVCVAZUUCUVQUUFUVRUUAKUUBUVCVBUVSUUDUVEUUEUUBUVCAV
      CVDVEYTUUTVAZUVQUVDUVRUVGUVTUUAUVAKUVCYSLYTUUTVBVFUVTUUEUVFUVEYTUUTAVCVGV
      EVHVIVJUURYIUVDUVGYLUURYIUVDUURYIUMZYSKLUVCUUTUWAYPUVCPZYQUUTPZUMZYPEPZYP
      UUBYFDUBZVAZNEVKZUMZYQEPZYQYTYGDUBZVAZMEVKZUMZUMZYSUURUWDUWOUIZYIYAUUQUWP
      YDUVJUWBUWIUWCUWNYAUUOUWBUWIUIUUPYAUUOUMZUWIKUVCUWQUVCUWHKEVLUWIKVMKNYFBC
      DEHIJVNUWHKEVOVPVQUSYAUUPUWCUWNUIUUOYAUUPUMZUWNLUUTUWRUUTUWMLEVLUWNLVMLMY
      GBCDEHIJVNUWMLEVOVPVQUTVRVJVSUWOUWEUWJUMZUWGUWLUMZMEVKNEVKZUMZUWAYSUXBUWS
      UWHUWMUMZUMUWOUXAUXCUWSUWGUWLNMEEVTWAUWEUWJUWHUWMWBWCUWAUXAYSUWSUWAUWTYSN
      MEEUWAUUBEPZYTEPZUMZUWTYSUDUWAUXFUMZYSUWTUWFUWKDUBZAPUXGUUBYTDUBZYHDUBZUX
      HAUURUXFUXJUXHVAZYIYAUUQUXFUXKYDYAUUQUXFUXKYAUXFUUQUXKUUBYTYFYGBCDEHIJWDW
      EWFWGVJUUNYIUXFUXJAPZUUQUUNYIUMUXFUXIEPZUXLUUNUXFUXMYIYAUXFUXMYDYAUXDUXEU
      XMYAUUJUXDUXEUXMUUKUUBYTBCDEHIJWHWIWJVJVJUUNYIUXMUXLYAUUJYDYIUXMUMUXLUUKY
      HUXIBCDAEHIJWKWLWMWNWGWOUWTYRUXHAYPUWFYQUWKDWPWQWRWSWTXAXBXCXDWSYAUUQUVGY
      LUDYDUVJUVEYJUVFYKUVJYFUVCPZUVEYJYAUUOUXNUUPUWQUVBUVCUGZUXNYAUUJUVMUXOUUO
      UUKUVNBUVBCEHJXEURYFUVCFXFXGXHUSUVCAYFXIXJUVJYGUUTPZUVFYKYAUUPUXPUUOUWRUU
      SUUTUGZUXPYAUUJUVOUXQUUPUUKUVPBUUSCEHJXEURYGUUTGXFXGXHUTUUTAYGXIXJXKVJXLX
      MXNWSXOXPYDYEUUHXQYDYEYNXQXRXCYAUUJYOYBUDUUKUUJYOYBABCDEFGHIJXSWSULXT $.
      $( [19-Jun-2010] $)

    $d A a b $.  $d B b $.
    $( Property of a prime ideal in a commutative ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    pridlc $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
          ( A e. X /\ B e. X /\ ( A H B ) e. P ) ) -> ( A e. P \/ B e. P ) ) $=
      ( va vb ccring wcel cpridl cfv wa cv co wo wi wral w3a cidl wne ispridlc
      biimpa simp3d wceq opreq1 eleq1d eleq1 orbi1d imbi12d opreq2 orbi2d
      rcla42v com12 exp3a 3imp2 sylan ) DMNZCDOPNZQZKRZLRZFSZCNZVECNZVFCNZTZUAZ
      LGUBKGUBZAGNZBGNZABFSZCNZUCACNZBCNZTZVDCDUDPNZCGUEZVMVBVCWAWBVMUCCDEFGKLH
      IJUFUGUHVMVNVOVQVTVMVNVOVQVTUAZVNVOQVMWCVLWCAVFFSZCNZVRVJTZUAKLABGGVEAUIZ
      VHWEVKWFWGVGWDCVEAVFFUJUKWGVIVRVJVEACULUMUNVFBUIZWEVQWFVTWHWDVPCVFBAFUOUK
      WHVJVSVRVFBCULUPUNUQURUSUTVA $.
      $( [17-Jun-2011] $)

    $( Property of a prime ideal in a commutative ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    pridlc2 $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
            ( A e. ( X \ P ) /\ B e. X /\ ( A H B ) e. P ) ) -> B e. P ) $=
      ( ccring wcel cpridl cfv wa cdif co w3a wn eldifn 3ad2ant1 adantl wi
      eldifi pridlc ord syl3anr1 mpd ) DKLCDMNLOZAGCPLZBGLZABFQCLZRZOACLZSZBCLZ
      UMUOUIUJUKUOULAGCTUAUBUJAGLZUIUKULUOUPUCAGCUDUIUQUKULROUNUPABCDEFGHIJUEUF
      UGUH $.
      $( [17-Jun-2011] $)

    $( Property of a prime ideal in a commutative ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    pridlc3 $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
          ( A e. ( X \ P ) /\ B e. ( X \ P ) ) ) -> ( A H B ) e. ( X \ P ) ) $=
      ( ccring wcel cpridl cfv wa cdif co wn cring crngrng eldifi anim12i
      ringocl 3expb syl2an adantlr eldifn ad2antll wi pridlc2 3exp2 imp32
      con3d sylanr2 mpd eldif sylanbrc ) DKLZCDMNLZOZAGCPZLZBVALZOZOZABFQZGLZVF
      CLZRZVFVALURVDVGUSURDSLZAGLZBGLZOVGVDDTVBVKVCVLAGCUABGCUAZUBVJVKVLVGABDEF
      GHIJUCUDUEUFVEBCLZRZVIVCVOUTVBBGCUGUHVCUTVBVLVOVIUIVMUTVBVLOOVHVNUTVBVLVH
      VNUIUTVBVLVHVNABCDEFGHIJUJUKULUMUNUOVFGCUPUQ $.
      $( [17-Jun-2011] $)
  $}

  ${
    $d R a b $.  $d Z a b $.  $d H a b $.  $d X a b $.
    isdmn3.1 $e |- G = ( 1st ` R ) $.
    isdmn3.2 $e |- H = ( 2nd ` R ) $.
    isdmn3.3 $e |- X = ran G $.
    isdmn3.4 $e |- Z = ( Id ` G ) $.
    isdmn3.5 $e |- U = ( Id ` H ) $.
    $( The predicate "is a domain", alternate expression.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    isdmn3 $p |- ( R e. Dmn <-> ( R e. CRing /\ U =/= Z /\ A. a e. X A. b e. X
                                ( ( a H b ) = Z -> ( a = Z \/ b = Z ) ) ) ) $=
      ( cdmn wcel cprrng ccring wa wne cv co wceq wo wi wral w3a isdmn2 cring
      csn cpridl cfv isprrng cidl ispridlc crngrng biantrurd 3anass 0idl syl
      wb crn c1st rneqi eqtri ring1cl eleq2 elsni syl6bir syl5com c1o cen wbr
      uznzr ring0cl en1eqsn eqcomd ex sylbird impbid necon3bid oprex elsnc
      elsn orbi12i imbi12i a1i 2ralbidv anbi12d bitr3d syl5bb 3bitr3d pm5.32i
      ancom 3bitr4i bitri ) ANOAPOZAQOZRZWQBFSZGTZHTZDUAZFUBZWTFUBZXAFUBZUCZUDZ
      HEUEGEUEZUFZAUGWQWPRWQWSXHRZRWRXIWQWPXJWPAUHOZFUIZAUJUKOZRZWQXJACFILULWQX
      MXLAUMUKOZXLESZXBXLOZWTXLOZXAXLOZUCZUDZHEUEGEUEZUFZXNXJXLACDEGHIJKUNWQXKX
      MAUOZUPYCXOXPYBRZRZWQXJXOXPYBUQWQYEYFXJWQXOYEWQXKXOYDACFILURUSUPWQXPWSYBX
      HWQXLEBFWQXKXLEUBZBFUBZUTYDXKYGYHXKBEOZYGYHABDEECVAAVBUKZVAKCYJIVCVDJMVEY
      GYIBXLOYHXLEBVFBFVGVHVIXKYHEVJVKVLZYGABCDEFIJLMKVMXKFEOZYKYGUDACEFIKLVNYL
      YKYGYLYKREXLFEVOVPVQUSVRVSUSVTWQYAXGGHEEYAXGUTWQXQXCXTXFXBFWTXADWAWBXRXDX
      SXEGFWCHFWCWDWEWFWGWHWIWJWKWJWLWPWQWMWQWSXHUQWNWO $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R a b $.  $d H a b $.  $d X a b $.  $d Z a b $.  $d A a b $.  $d B b $.
    dmnnzd.1 $e |- G = ( 1st ` R ) $.
    dmnnzd.2 $e |- H = ( 2nd ` R ) $.
    dmnnzd.3 $e |- X = ran G $.
    dmnnzd.4 $e |- Z = ( Id ` G ) $.
    $( A domain has no zero-divisors (besides zero).  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    dmnnzd $p |- ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ ( A H B ) = Z ) )
                                                  -> ( A = Z \/ B = Z ) ) $=
      ( va vb cdmn wcel co wceq wo wi cv wral wa ccring cgi cfv wne eqid
      isdmn3 simp3bi opreq1 eqeq1d eqeq1 orbi1d imbi12d opreq2 orbi2d rcla42v
      syl5com exp3a 3imp2 ) CNOZAFOZBFOZABEPZGQZAGQZBGQZRZVAVBVCVEVHSZVALTZMTZE
      PZGQZVJGQZVKGQZRZSZMFUALFUAZVBVCUBVIVACUCOEUDUEZGUFVRCVSDEFGLMHIJKVSUGUHU
      IVQVIAVKEPZGQZVFVORZSLMABFFVJAQZVMWAVPWBWCVLVTGVJAVKEUJUKWCVNVFVOVJAGULUM
      UNVKBQZWAVEWBVHWDVTVDGVKBAEUOUKWDVOVGVFVKBGULUPUNUQURUSUT $.
      $( [19-Jun-2010] $)
  $}

  ${
    dmncan.1 $e |- G = ( 1st ` R ) $.
    dmncan.2 $e |- H = ( 2nd ` R ) $.
    dmncan.3 $e |- X = ran G $.
    dmncan.4 $e |- Z = ( Id ` G ) $.
    $( Cancellation law for domains.  (Contributed by Jeff Madsen,
       6-Jan-2011.) $)
    dmncan1 $p |- ( ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ C e. X ) )
                      /\ A =/= Z ) -> ( ( A H B ) = ( A H C ) -> B = C ) ) $=
      ( cdmn wcel w3a wa wne co cgs cfv wceq cring dmnrng eqid ringsubdi sylan
      adantr eqeq1d wi wo cgr ringogrpo syl grpdivcl 3expb adantlr dmnnzd
      3exp2 imp31 syldan exp43 3imp2 neor syl6ib com23 imp sylbird wb ringocl
      3adant3r3 3adant3r2 grpeqdivid syl111anc 3adantr1 3imtr4d ) DMNZAGNZBGNZC
      GNZOZPZAHQZPZABFRZACFRZESTZRZHUAZBCWFRZHUAZWDWEUAZBCUAZWCWHAWIFRZHUAZWJWC
      WMWGHWAWMWGUAZWBVPDUBNZVTWODUCZABCWFDEFGIJKWFUDZUEUFUGUHWAWBWNWJUIWAWNWBW
      JWAWNAHUAWJUJZWBWJUIVPVQVRVSWNWSUIZVPVQVRVSWTVPVQPVRVSPZWIGNZWTVPXAXBVQVP
      EUKNZXAXBVPWPXCWQDEIULUMZXCVRVSXBBCWFEGKWRUNUOUFUPVPVQXBWTVPVQXBWNWSAWIDE
      FGHIJKLUQURUSUTVAVBWJAHVCVDVEVFVGWAWKWHVHZWBWAXCWDGNZWEGNZXEVPXCVTXDUGVPW
      PVTXFWQWPVQVRXFVSABDEFGIJKVIVJUFVPWPVTXGWQWPVQVSXGVRACDEFGIJKVIVKUFWDWEWF
      HEGKLWRVLVMUGWAWLWJVHZWBVPVRVSXHVQVPXCXAXHXDXCVRVSXHBCWFHEGKLWRVLUOUFVNUG
      VO $.
      $( [6-Jan-2011] $)

    $( Cancellation law for domains.  (Contributed by Jeff Madsen,
       6-Jan-2011.) $)
    dmncan2 $p |- ( ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ C e. X ) )
                      /\ C =/= Z ) -> ( ( A H C ) = ( B H C ) -> A = B ) ) $=
      ( cdmn wcel w3a wa wne co wceq wb ccring dmncrng crngcom 3adant3r2
      3adant3r1 eqeq12d sylan adantr wi 3anrot biimpri dmncan1 sylanl2 sylbid
      ) DMNZAGNZBGNZCGNZOZPZCHQZPACFRZBCFRZSZCAFRZCBFRZSZABSZUTVDVGTZVAUODUANZU
      SVIDUBVJUSPVBVEVCVFVJUPURVBVESUQACDEFGIJKUCUDVJUQURVCVFSUPBCDEFGIJKUCUEUF
      UGUHUSUOURUPUQOZVAVGVHUIVKUSURUPUQUJUKCABDEFGHIJKLULUMUN $.
      $( [6-Jan-2011] $)
  $}
$( (End of Jeff Madsen's mathbox.) $)


