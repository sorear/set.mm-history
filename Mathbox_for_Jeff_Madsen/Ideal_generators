$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ideal generators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c IdlGen $.

  $( Extend class notation with the ideal generation function. $)
  cigen $a class IdlGen $.

  ${
    $d r s i j $.
    $( Define the ideal generated by a subset of a ring. $)
    df-igen $a |- IdlGen = { <. <. r , s >. , i >. |
                  ( ( r e. Ring /\ s e. ~P ran ( 1st ` r ) )
                      /\ i = |^| { j e. ( Idl ` r ) | s C_ j } ) } $.
  $}

  ${
    $d R r s i j $.  $d S r s i j $.  $d X r s i j $.  $d I j $.
    igenval.1 $e |- G = ( 1st ` R ) $.
    igenval.2 $e |- X = ran G $.
    $( The ideal generated by a subset of a ring. $)
    igenval $p |- ( ( R e. Ring /\ S C_ X ) -> ( R IdlGen S ) =
                                        |^| { j e. ( Idl ` R ) | S C_ j } ) $=
      ( vr vs vi cring wcel wss wa cvv cv cidl cfv crab cint w3a cigen co wceq
      pm3.26 crn c1st fvex eqeltri rnex ssex adantl c0 wne wrex sseq2 rcla4ev
      rngidl sylan rabn0 sylibr intex sylib 3jca cpw wi eleq1 fveq2 syl6eqr
      rneqd pweq syl eleq2d anbi12d rabeq inteqd eqeq2d anbi2d sseq1 rabbidv
      pm3.21 impbid2 moeq moani df-igen oprabvaligg com12 elpw2 sylan2br mpd )
      AKLZBEMZNZWKBOLZBCPZMZCAQRZSZTZOLZUAZABUBUCWSUDZWMWKWNWTWKWLUEWLWNWKBEEDU
      FZOGDDAUGRZOFAUGUHUIUJUIZUKULWMWRUMUNZWTWMWPCWQUOZXFEWQLWLXGWKWPWLCEWQWOE
      BUPUQADEFGURUSWPCWQUTVAWRVBVCVDWKBEVEZLZXAXBVFWLXAWKXINZXBHPZKLZIPZXKUGRZ
      UFZVEZLZNZJPZXMWOMZCXKQRZSZTZUDZNWKXMXHLZNZXSXTCWQSZTZUDZNXJXSWSUDZNZXJHI
      JABWSOKOUBXKAUDZXRYFYDYIYLXLWKXQYEXKAKVGYLXPXHXMYLXOEUDXPXHUDYLXOXCEYLXND
      YLXNXDDXKAUGVHFVIVJGVIXOEVKVLVMVNYLYCYHXSYLYBYGYLYAWQUDYBYGUDXKAQVHXTCYAW
      QVOVLVPVQVNXMBUDZYFXJYIYJYMYEXIWKXMBXHVGVRYMYHWSXSYMYGWRYMXTWPCWQXMBWOVSV
      TVPVQVNYJYKXJXJYJUEYJXJWAWBYDXRJJYCWCWDJCIHWEWFWGBEXEWHWIWJ $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)

    $( A set is a subset of the ideal it generates. $)
    igenss $p |- ( ( R e. Ring /\ S C_ X ) -> S C_ ( R IdlGen S ) ) $=
      ( vj cring wcel wss wa cigen co cv cidl cfv crab cint ssintub igenval
      sseq2d mpbiri ) AHIBDJKZBABLMZJBBGNJGAOPZQRZJGBUESUCUDUFBABGCDEFTUAUB $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)

    $( The ideal generated by a set is an ideal. $)
    igenidl $p |- ( ( R e. Ring /\ S C_ X ) ->
                              ( R IdlGen S ) e. ( Idl ` R ) ) $=
      ( vj cring wcel wss wa cigen co cv cidl cfv crab cint igenval c0 wne
      wrex sseq2 rcla4ev rngidl sylan rabn0 sylibr ssrab2 intidl mp3an3 syldan
      eqeltrd ) AHIZBDJZKZABLMBGNZJZGAOPZQZRZUSABGCDEFSUNUOUTTUAZVAUSIZUPURGUSU
      BZVBDUSIUOVDUNURUOGDUSUQDBUCUDACDEFUEUFURGUSUGUHUNVBUTUSJVCURGUSUIUTAUJUK
      ULUM $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)
  $}

  ${
    $d R j $.  $d S j $.  $d I j $.
    $( The ideal generated by a set is the minimal ideal containing that
       set. $)
    igenmin $p |- ( ( R e. Ring /\ I e. ( Idl ` R ) /\ S C_ I )
                                              -> ( R IdlGen S ) C_ I ) $=
      ( vj cring wcel cidl cfv wss w3a cigen co cv crab cint wceq c1st crn
      eqid idlss wa igenval sstr ancoms sylan2 anassrs syldanl 3impa sseq2
      elrab intss1 sylbir 3adant1 eqsstrd ) AEFZCAGHZFZBCIZJABKLZBDMZIZDUPNZOZC
      UOUQURUSVCPZUOUQCAQHZRZIZURVDAVECVFVESZVFSZTUOVGURVDUOBVFIZVDVGURUAABDVEV
      FVHVIUBURVGVJBCVFUCUDUEUFUGUHUQURVCCIZUOUQURUACVBFVKVAURDCUPUTCBUIUJCVBUK
      ULUMUN $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)

    $( The ideal generated by an ideal is that ideal. $)
    igenidl2 $p |- ( ( R e. Ring /\ I e. ( Idl ` R ) )
                                          -> ( R IdlGen I ) = I ) $=
      ( vj cring wcel cidl cfv wa cigen co cv wss crab cint c1st crn wceq eqid
      idlss igenval syldan intmin adantl eqtrd ) ADEZBAFGZEZHABIJZBCKLCUFMNZBUE
      UGBAOGZPZLUHUIQAUJBUKUJRZUKRZSABCUJUKULUMTUAUGUIBQUECBUFUBUCUD $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)
  $}

  ${
    $d R i j $.  $d S i j $.  $d I j $.  $d X i $.
    igenval2.1 $e |- G = ( 1st ` R ) $.
    igenval2.2 $e |- X = ran G $.
    $( The ideal generated by a subset of a ring. $)
    igenval2 $p |- ( ( R e. Ring /\ S C_ X ) -> ( ( R IdlGen S ) = I <->
                                  ( I e. ( Idl ` R ) /\ S C_ I /\
                          A. j e. ( Idl ` R ) ( S C_ j -> I C_ j ) ) ) ) $=
      ( vi cring wcel wss wa cigen co wceq cidl cfv cv wi wral w3a eleq1 sseq2
      sseq1 imbi2d ralbidv 3anbi123d igenidl igenss igenmin 3expia r19.21aiva
      adantr 3jca syl5cbi 3adant3r3 adantlr crab cint ralrab biimpri ssint
      sylibr 3ad2ant3 adantl igenval sseqtr4d eqssd ex impbid ) AJKZBFLZMZABNOZ
      EPZEAQRZKZBELZBCSZLZEVTLZTZCVQUAZUBZVPVOVQKZBVOLZWAVOVTLZTZCVQUAZUBWEVNVP
      WFVRWGVSWJWDVOEVQUCVOEBUDVPWIWCCVQVPWHWBWAVOEVTUEUFUGUHVNWFWGWJABDFGHUIAB
      DFGHUJVLWJVMVLWICVQVLVTVQKWAWHABVTUKULUMUNUOUPVNWEVPVNWEMZVOEVLWEVOELZVMV
      LVRVSWLWDABEUKUQURWKEBISZLZIVQUSZUTZVOWEEWPLZVNWDVRWQVSWDWBCWOUAZWQWRWDWN
      WAWBCIVQWMVTBUDVAVBCEWOVCVDVEVFVNVOWPPWEABIDFGHVGUNVHVIVJVK $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)
  $}

  ${
    $d R j x y u v w r s $.  $d X j x y u v w r s $.  $d G x y r s $.
    $d H j x y u v w r s $.  $d A j x y u v w r s $.
    prnc.1 $e |- G = ( 1st ` R ) $.
    prnc.2 $e |- H = ( 2nd ` R ) $.
    prnc.3 $e |- X = ran G $.
    $( A principal ideal (an ideal generated by one element) in a commutative
       ring. $)
    prnc $p |- ( ( R e. CRing /\ A e. X ) -> ( R IdlGen { A } ) =
                                  { x e. X | E. y e. X x = ( y H A ) } ) $=
      ( vj vu vv vw vr vs ccring wcel wa csn cigen co cv wceq wrex crab cidl
      cfv wss wi wral w3a cgi cring ssrab2 a1i eqeq1 rexbidv elrab eqid
      ring0cl adantr opreq1 eqeq2d rcla4ev ringlz eqcomd sylanc sylanbrc
      eleq1d ralbidv opreq2 anbi12d ringdir 3exp2 imp42 ringcl 3expa mpan2
      ad2antlr ringgcl 3expib imdistani sylan eqeltrrd an1rs anassrs syl5cbir
      r19.23adva adantld cbvrexv syl6bb syl5ib r19.21aiv ringass anass1rs
      r19.21aiva jca 3jca crngrng wb isidlc mpbird pm3.27 crn c1st rneqi eqtri
      ring1cl ringlidm snssd eleq1 idllmulcl rabss sylibr ex snssg biimpar
      syl5 expdimp igenval2 snssi syl2an ) DQRZCGRZSZDCTZUAUBAUCZBUCZCFUBZUDZBG
      UEZAGUFZUDZYMDUGUHZRZYGYMUIZYGKUCZUIZYMYRUIZUJZKYOUKZULZYFYPYQUUBYFYPYMGU
      IZEUMUHZYMRZLUCZMUCZEUBZYMRZMYMUKZNUCZUUGFUBZYMRZNGUKZSZLYMUKZULZDUNRZYEU
      URYDUUSYESZUUDUUFUUQUUDUUTYLAGUOUPUUFUUEGRZUUEYJUDZBGUEZUUTYLUVCAUUEGYHUU
      EUDYKUVBBGYHUUEYJUQURUSUUSUVAYEDEGUUEHJUUEUTZVAVBZUVAUUEUUECFUBZUDZUVCUUT
      UVBUVGBUUEGYIUUEUDYJUVFUUEYIUUECFVCVDVEUVEUUTUVFUUECDEFGUUEUVDJHIVFVGVHVI
      UUTUUPLYMUUTUUGGRZUUGOUCZCFUBZUDZOGUEZSUUPUUGYMRUUTUVLUUPUVHUUTUVKUUPOGUV
      KUUPUVJUUHEUBZYMRZMYMUKZUULUVJFUBZYMRZNGUKZSUUTUVIGRZSZUVKUUKUVOUUOUVRUVK
      UUJUVNMYMUVKUUIUVMYMUUGUVJUUHEVCVJVKUVKUUNUVQNGUVKUUMUVPYMUUGUVJUULFVLVJV
      KVMUVTUVOUVRUVTUVNMYMUVTUUHGRZUUHPUCZCFUBZUDZPGUEZSUVNUUHYMRUVTUWEUVNUWAU
      VTUWDUVNPGUWDUVNUVJUWCEUBZYMRZUVTUWBGRZSUWDUVMUWFYMUUHUWCUVJEVLVJUUTUVSUW
      HUWGUUSUVSUWHSZYEUWGUUSUWISZYESUVIUWBEUBZCFUBZUWFYMUUSUVSUWHYEUWLUWFUDZUU
      SUVSUWHYEUWMUVIUWBCDEFGHIJVNVOVPUUSUWKGRZSZYEUWLYMRZUWJUWPUWLGRZUWLYJUDZB
      GUEZUWOYESYLUWSAUWLGYHUWLUDYKUWRBGYHUWLYJUQURUSUUSUWNYEUWQUWKCDEFGHIJVQVR
      UWNUWSUUSYEUWNUWLUWLUDZUWSUWLUTUWRUWTBUWKGYIUWKUDYJUWLUWLYIUWKCFVCVDVEVSV
      TVIUUSUWIUWNUUSUVSUWHUWNUVIUWBDEGHJWAWBWCWDWEWFWGWHWIWJYLUWEAUUHGYHUUHUDZ
      YLUUHYJUDZBGUEUWEUXAYKUXBBGYHUUHYJUQURUXBUWDBPGYIUWBUDYJUWCUUHYIUWBCFVCVD
      WKWLUSWMWNUVTUVQNGUUTUULGRZUVSUVQUUTUXCUVSSZSUULUVIFUBZCFUBZUVPYMUUSUXDYE
      UXFUVPUDZUUSUXCUVSYEUXGUUSUXCUVSYEUXGUULUVICDEFGHIJWOVOVPWFUUSUXDYEUXFYMR
      ZUUSUXEGRZSZYEUXHUUSUXDSUXHUXFGRZUXFYJUDZBGUEZUXJYESYLUXMAUXFGYHUXFUDYKUX
      LBGYHUXFYJUQURUSUUSUXIYEUXKUXECDEFGHIJVQVRUXIUXMUUSYEUXIUXFUXFUDZUXMUXFUT
      UXLUXNBUXEGYIUXEUDYJUXFUXFYIUXECFVCVDVEVSVTVIUUSUXDUXIUUSUXCUVSUXIUULUVID
      EFGHIJVQWBWCWDWFWEWPWQWRWHWIWJYLUVLAUUGGYHUUGUDZYLUUGYJUDZBGUEUVLUXOYKUXP
      BGYHUUGYJUQURUXPUVKBOGYIUVIUDYJUVJUUGYIUVICFVCVDWKWLUSWMWNWSDWTZWDYDYPUUR
      XAYELMNDEFYMGUUEHIJUVDXBVBXCYFCYMCYMRYECYJUDZBGUEZYFYLUXSACGYHCUDYKUXRBGY
      HCYJUQURUSYDYEXDUUSYEUXSYDFUMUHZGRZCUXTCFUBZUDZUXSUUTUXRUYCBUXTGYIUXTUDYJ
      UYBCYIUXTCFVCVDVEUUSUYAYEDUXTFGGEXEDXFUHZXEJEUYDHXGXHZIUXTUTZXIVBUUTUYBCC
      DUXTFGIUYEUYFXJVGVHUXQWDVIXKUUSYEUUBYDUUTUUAKYOUUSYRYORZYEUUAUUSUYGSZYEYS
      YTUYHCYRRZYTYEYSSUYHUYIYTUYHUYISZYLYHYRRZUJZAGUKYTUYJUYLAGUYJUYLYHGRUYJYK
      UYKBGYKUYKYJYRRZUYJYIGRZSYHYJYRXLUYHUYIUYNUYMCYIDEFYRGHIJXMWGWHWIVBWQYLAG
      YRXNXOXPYEUYIYSCYRGXQXRXSXTWFWQUXQWDWSUUSYGGUIYNUUCXAYDYEDYGKEYMGHJYAUXQC
      GYBYCXC $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)
  $}

  ${
    $d K x y z $.  $d X x y z $.  $d G y z $.  $d H x y z $.  $d U x y z $.
    $d Z x y $.
    isfldidl.1 $e |- G = ( 1st ` K ) $.
    isfldidl.2 $e |- H = ( 2nd ` K ) $.
    isfldidl.3 $e |- X = ran G $.
    isfldidl.4 $e |- Z = ( Id ` G ) $.
    isfldidl.5 $e |- U = ( Id ` H ) $.
    $( Determine if a ring is a field based on its ideals. $)
    isfldidl $p |- ( K e. Fld <-> ( K e. CRing /\
                                U =/= Z /\ ( Idl ` K ) = { { Z } , X } ) ) $=
      ( vy vx vz cfld wcel ccring wne cidl cfv csn cpr wceq w3a fldcrng cdrng
      flddivrng dvrunz syl divrngidl 3jca isfld2 cring cv co wrex cdif wral wa
      crngrng 3ad2ant1 3simp2 crab crn c1st rneqi eqtri ring1cl ad2antrr cigen
      wn wss eleq2 visset snss biimpri syl5cbi con3d imp igenss snssi sylan2
      sylan anasss eldif sylan2b adantlr wo igenidl eldifi snssd an1rs oprex
      elpr sylib ord mpd sylanl1 prnc eqtr3d eleqtrd eqeq1 rexbidv elrab
      pm3.27d eqcom rexbii sylibr r19.21aiva 3adant2 jca32 isdivrng3 3simp1
      sylanbrc impbii ) DOPZDQPZAFRZDSTZFUAZEUBZUCZUDZXPXQXRYBDUEXPDUFPZXRDUGZD
      ABCEFGHIJKUHUIXPYDYBYEDBCEFGHIJUJUIUKXPYDXQYCDULYCDUMPZXRLUNMUNZCUOZAUCZL
      EUPZMEXTUQZURZUSUSYDYCYFXRYLXQXRYFYBDUTZVAXQXRYBVBXQYBYLXRXQYBUSZYJMYKYNY
      GYKPZUSZAYHUCZLEUPZYJYPAEPZYRYPANUNZYHUCZLEUPZNEVCZPYSYRUSYPAEUUCXQYSYBYO
      XQYFYSYMDACEEBVDDVETZVDIBUUDGVFVGHKVHUIVIYPDYGUAZVJUOZEUUCYFYBYOUUFEUCZXQ
      YFYBUSYOUSZUUFXTUCZVKZUUGYFYOUUJYBYFYGEPZYGXTPZVKZUSUUJYOYFUUKUUMUUJUUEUU
      FVLZUUMUUJYFUUKUSUUNUUMUUJUUNUUIUULUUIYGUUFPZUULUUNUUFXTYGVMUUOUUNYGUUFMV
      NVOVPVQVRVSYFUUEEVLZUUNUUKDUUEBEGIVTYGEWAWBWCWDYGEXTWEWFWGUUHUUIUUGUUHUUF
      YAPZUUIUUGWHYFYOYBUUQYFYOUSZYBUUQYBUUFXSPZUUQUURXSYAUUFVMYFUUPUUSYODUUEBE
      GIWIYOYGEYGEXTWJZWKWBVQVSWLUUFXTEDUUEVJWMWNWOWPWQYMWRXQYOUUFUUCUCZYBXQUUK
      UVAYONLYGDBCEGHIWSUUTWBWGWTXAUUBYRNAEYTAUCUUAYQLEYTAYHXBXCXDWOXEYIYQLEYHA
      XFXGXHXIXJXKMLDABCEFGHJIKXLXHXQXRYBXMXNXO $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)
  $}

  ${
    isfldidl2.1 $e |- G = ( 1st ` K ) $.
    isfldidl2.2 $e |- H = ( 2nd ` K ) $.
    isfldidl2.3 $e |- X = ran G $.
    isfldidl2.4 $e |- Z = ( Id ` G ) $.
    $( Determine if a ring is a field based on its ideals. $)
    isfldidl2 $p |- ( K e. Fld <-> ( K e. CRing /\
                              X =/= { Z } /\ ( Idl ` K ) = { { Z } , X } ) ) $=
      ( cfld wcel ccring cgi cfv wne cidl csn cpr wceq w3a eqid isfldidl wa
      cring wb crngrng 0ring eqcom syl5bb syl necon3bid anbi1d pm5.32i 3anass
      3bitr4i bitri ) CJKCLKZBMNZEOZCPNEQZDRSZTZUQDUTOZVATZURABCDEFGHIURUAZUBUQ
      USVAUCZUCUQVCVAUCZUCVBVDUQVFVGUQUSVCVAUQUREDUTUQCUDKZURESZDUTSZUECUFVHEUR
      SVJVICURABDEFGHIVEUGUREUHUIUJUKULUMUQUSVAUNUQVCVAUNUOUP $.
      $( [25-Jun-2011] $) $( [6-Jan-2011] $)
  $}

  ${
    $d R a b x y r s $.  $d P a b x y r s $.  $d X a b x y r s $.
    $d G x y r s $.  $d H a b x y r s $.
    ispridlc.1 $e |- G = ( 1st ` R ) $.
    ispridlc.2 $e |- H = ( 2nd ` R ) $.
    ispridlc.3 $e |- X = ran G $.
    $( The predicate "is a prime ideal".  Alternate definition for commutative
       rings. $)
    ispridlc $p |- ( R e. CRing -> ( P e. ( PrIdl ` R ) <-> ( P e. ( Idl ` R )
                              /\ P =/= X /\ A. a e. X A. b e. X
                      ( ( a H b ) e. P -> ( a e. P \/ b e. P ) ) ) ) ) $=
      ( vx vy vs vr ccring wcel cpridl cfv cidl wne cv co wo wi wral w3a wss
      cring wb crngrng ispridl syl wa csn cigen wceq raleq sseq1 orbi1d
      imbi12d ralbidv orbi2d rcla42v igenidl snssi syl2an adantrr adantrl
      sylanc adantlr wrex crab cab prnc df-rab syl6eq abeq2d anbi12d adantr
      opreq12 eleq1d crngm4 3com23 3expa adantllr ringcl syl3an1 3expb
      idllmulcl sylanl1 anassrs syldan eqeltrrd syl5cbir ex r19.23advv adantld
      reeanv anbi2i an4 bitri syl5ibr sylbid r19.21aivv ssel igenss visset
      snss sylibr syl5com orim12d imim12d syld r19.21advva adantrd imdistand
      df-3an 3imtr4g ispridl2 impbid ) BOPZABQRPZABSRZPZAETZFUAZGUAZDUBZAPZYFAP
      ZYGAPZUCZUDZGEUEFEUEZUFZYAYBYDYEKUAZLUAZDUBZAPZLMUAZUEZKNUAZUEZUUBAUGZYTA
      UGZUCZUDZMYCUENYCUEZUFZYOYABUHPZYBUUIUIBUJZKLABCDENMHIJUKULYAYDYEUMZUUHUM
      UULYNUMUUIYOYAUULUUHYNYAYDUUHYNUDZYEYAYDUUMYAYDUMZUUHYMFGEEUUNYFEPZYGEPZU
      MZUMZUUHYSLBYGUNZUOUBZUEZKBYFUNZUOUBZUEZUVCAUGZUUTAUGZUCZUDZYMYAUUQUUHUVH
      UDZYDUVCYCPZUUTYCPZUVIYAUUQUMZUUGUVHUUAKUVCUEZUVEUUEUCZUDNMUVCUUTYCYCUUBU
      VCUPZUUCUVMUUFUVNUUAKUUBUVCUQUVOUUDUVEUUEUUBUVCAURUSUTYTUUTUPZUVMUVDUVNUV
      GUVPUUAUVAKUVCYSLYTUUTUQVAUVPUUEUVFUVEYTUUTAURVBUTVCYAUUOUVJUUPUUJUVBEUGZ
      UVJYAUUOBUVBCEHJVDUUKYFEVEZVFVGYAUUPUVKUUOUUJUUSEUGZUVKYAUUPBUUSCEHJVDUUK
      YGEVEZVFVHVIVJUURYIUVDUVGYLUURYIUVDUURYIUMZYSKLUVCUUTUWAYPUVCPZYQUUTPZUMZ
      YPEPZYPUUBYFDUBZUPZNEVKZUMZYQEPZYQYTYGDUBZUPZMEVKZUMZUMZYSUURUWDUWOUIZYIY
      AUUQUWPYDUVLUWBUWIUWCUWNYAUUOUWBUWIUIUUPYAUUOUMZUWIKUVCUWQUVCUWHKEVLUWIKV
      MKNYFBCDEHIJVNUWHKEVOVPVQVGYAUUPUWCUWNUIUUOYAUUPUMZUWNLUUTUWRUUTUWMLEVLUW
      NLVMLMYGBCDEHIJVNUWMLEVOVPVQVHVRVJVSUWAUWEUWJUMZUWGUWLUMZMEVKNEVKZUMZYSUW
      OUWAUXAYSUWSUWAUWTYSNMEEUWAUUBEPZYTEPZUMZUWTYSUDUWTYSUWFUWKDUBZAPUWAUXEUM
      ZUWTYRUXFAYPUWFYQUWKDVTWAUXGUUBYTDUBZYHDUBZUXFAUURUXEUXIUXFUPZYIYAUUQUXEU
      XJYDYAUUQUXEUXJYAUXEUUQUXJUUBYTYFYGBCDEHIJWBWCWDWEVJUUNYIUXEUXIAPZUUQUUNY
      IUMUXEUXHEPZUXKUUNUXEUXLYIYAUXEUXLYDYAUXCUXDUXLUUJUXCUXDUXLYAUUBYTBCDEHIJ
      WFUUKWGWHVJVJUUNYIUXLUXKUUJYDYIUXLUMUXKYAYHUXHBCDAEHIJWIUUKWJWKWLWEWMWNWO
      WPWQUXBUWSUWHUWMUMZUMUWOUXAUXMUWSUWGUWLNMEEWRWSUWEUWJUWHUWMWTXAXBXCXDWOYA
      UUQUVGYLUDYDUVLUVEYJUVFYKUVEYFUVCPZYJUVLUVCAYFXEYAUUOUXNUUPUWQUVBUVCUGZUX
      NUUJUVQUXOYAUUOBUVBCEHJXFUUKUVRVFYFUVCFXGXHXIVGXJUVFYGUUTPZYKUVLUUTAYGXEY
      AUUPUXPUUOUWRUUSUUTUGZUXPUUJUVSUXQYAUUPBUUSCEHJXFUUKUVTVFYGUUTGXGXHXIVHXJ
      XKVJXLXMXNWOXOXPYDYEUUHXQYDYEYNXQXRXCYAUUJYOYBUDUUKUUJYOYBABCDEFGHIJXSWOU
      LXT $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)

    $d A a b $.  $d B b $.
    $( Property of a prime ideal in a commutative ring. $)
    pridlc $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
          ( A e. X /\ B e. X /\ ( A H B ) e. P ) ) -> ( A e. P \/ B e. P ) ) $=
      ( va vb cv co wcel wo wi wral w3a ccring cpridl cfv wa wceq opreq1
      eleq1d eleq1 orbi1d imbi12d opreq2 orbi2d rcla42v com12 exp3a 3imp2 cidl
      wne ispridlc biimpa 3simp3d sylan ) KMZLMZFNZCOZVBCOZVCCOZPZQZLGRKGRZAGOZ
      BGOZABFNZCOZSACOZBCOZPZDTOZCDUAUBOZUCZVJVKVLVNVQVJVKVLVNVQQZVKVLUCVJWAVIW
      AAVCFNZCOZVOVGPZQKLABGGVBAUDZVEWCVHWDWEVDWBCVBAVCFUEUFWEVFVOVGVBACUGUHUIV
      CBUDZWCVNWDVQWFWBVMCVCBAFUJUFWFVGVPVOVCBCUGUKUIULUMUNUOVTCDUPUBOZCGUQZVJV
      RVSWGWHVJSCDEFGKLHIJURUSUTVA $.
      $( [25-Jun-2011] $) $( [17-Jun-2011] $)

    $( Property of a prime ideal in a commutative ring. $)
    pridlc2 $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
            ( A e. ( X \ P ) /\ B e. X /\ ( A H B ) e. P ) ) -> B e. P ) $=
      ( ccring wcel cpridl cfv wa cdif co w3a wn eldifn 3ad2ant1 adantl wi
      pridlc ord eldifi syl3anr1 mpd ) DKLCDMNLOZAGCPLZBGLZABFQCLZRZOACLZSZBCLZ
      UMUOUIUJUKUOULAGCTUAUBUIAGLZUKULUOUPUCUJUIUQUKULROUNUPABCDEFGHIJUDUEAGCUF
      UGUH $.
      $( [25-Jun-2011] $) $( [17-Jun-2011] $)

    $( Property of a prime ideal in a commutative ring. $)
    pridlc3 $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
          ( A e. ( X \ P ) /\ B e. ( X \ P ) ) ) -> ( A H B ) e. ( X \ P ) ) $=
      ( co cdif wcel wn ccring cpridl cfv wa eldif cring ringcl 3expb crngrng
      eldifi anim12i syl2an adantlr eldifn ad2antll wi pridlc2 3exp2 imp32
      con3d sylanr2 mpd sylanbrc ) ABFKZGCLZMURGMZURCMZNZDOMZCDPQMZRZAUSMZBUSMZ
      RZRZURGCSVCVHUTVDDTMZAGMZBGMZRUTVCVHVJVKVLUTABDEFGHIJUAUBDUCVFVKVGVLAGCUD
      BGCUDZUEUFUGVIBCMZNZVBVGVOVEVFBGCUHUIVEVFVLVOVBUJVGVEVFVLRRVAVNVEVFVLVAVN
      UJVEVFVLVAVNABCDEFGHIJUKULUMUNVMUOUPUQ $.
      $( [25-Jun-2011] $) $( [17-Jun-2011] $)
  $}

  ${
    $d R a b $.  $d Z a b $.  $d H a b $.  $d X a b $.
    isdmn3.1 $e |- G = ( 1st ` R ) $.
    isdmn3.2 $e |- H = ( 2nd ` R ) $.
    isdmn3.3 $e |- X = ran G $.
    isdmn3.4 $e |- Z = ( Id ` G ) $.
    isdmn3.5 $e |- U = ( Id ` H ) $.
    $( The predicate "is a domain", alternate expression. $)
    isdmn3 $p |- ( R e. Dmn <-> ( R e. CRing /\ U =/= Z /\ A. a e. X A. b e. X
                                ( ( a H b ) = Z -> ( a = Z \/ b = Z ) ) ) ) $=
      ( cdmn wcel cprrng ccring wa wne cv co wceq wo wi wral w3a isdmn2 cring
      csn cpridl cfv cidl ispridlc crngrng biantrurd 0idl syl wb eleq2 elsni
      syl6bir crn c1st rneqi eqtri ring1cl syl5com c1o cen wbr uznzr ring0cl
      setwoe eqcomd ex sylbird impbid necon3bid oprex elsnc elsn orbi12i
      imbi12i a1i 2ralbidv anbi12d bitr3d 3anass syl5bb 3bitr3d isprrng
      pm5.32i ancom 3bitr4i bitri ) ANOAPOZAQOZRZWQBFSZGTZHTZDUAZFUBZWTFUBZXAFU
      BZUCZUDZHEUEGEUEZUFZAUGWQWPRWQWSXHRZRWRXIWQWPXJWQAUHOZFUIZAUJUKOZRZXJWPWQ
      XMXLAULUKOZXLESZXBXLOZWTXLOZXAXLOZUCZUDZHEUEGEUEZUFZXNXJXLACDEGHIJKUMWQXK
      XMAUNZUOWQXOXPYBRZRZXJYCWQYEYFXJWQXOYEWQXKXOYDACFILUPUQUOWQXPWSYBXHWQXLEB
      FWQXKXLEUBZBFUBZURYDXKYGYHYGBEOZYHXKYGYIBXLOYHXLEBUSBFUTVAABDEECVBAVCUKZV
      BKCYJIVDVEJMVFVGXKYHEVHVIVJZYGABCDEFIJLMKVKXKFEOZYKYGUDACEFIKLVLYLYKYGYLY
      KREXLFEVMVNVOUQVPVQUQVRWQYAXGGHEEYAXGURWQXQXCXTXFXBFWTXADVSVTXRXDXSXEGFWA
      HFWAWBWCWDWEWFWGXOXPYBWHWIWJACFILWKWIWLWPWQWMWQWSXHWHWNWO $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)
  $}

  ${
    $d R a b $.  $d H a b $.  $d X a b $.  $d Z a b $.  $d A a b $.  $d B b $.
    dmnnzd.1 $e |- G = ( 1st ` R ) $.
    dmnnzd.2 $e |- H = ( 2nd ` R ) $.
    dmnnzd.3 $e |- X = ran G $.
    dmnnzd.4 $e |- Z = ( Id ` G ) $.
    $( A domain has no zero-divisors (besides zero). $)
    dmnnzd $p |- ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ ( A H B ) = Z ) )
                                                  -> ( A = Z \/ B = Z ) ) $=
      ( va vb cdmn wcel co wceq wo wi wa cv wral opreq1 eqeq1d eqeq1 orbi1d
      imbi12d opreq2 orbi2d rcla42v ccring cgi cfv wne eqid isdmn3 3simp3bi
      syl5com exp3a 3imp2 ) CNOZAFOZBFOZABEPZGQZAGQZBGQZRZVAVBVCVEVHSZVBVCTLUAZ
      MUAZEPZGQZVJGQZVKGQZRZSZMFUBLFUBZVIVAVQVIAVKEPZGQZVFVORZSLMABFFVJAQZVMVTV
      PWAWBVLVSGVJAVKEUCUDWBVNVFVOVJAGUEUFUGVKBQZVTVEWAVHWCVSVDGVKBAEUHUDWCVOVG
      VFVKBGUEUIUGUJVACUKOEULUMZGUNVRCWDDEFGLMHIJKWDUOUPUQURUSUT $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)
  $}

  ${
    dmncan.1 $e |- G = ( 1st ` R ) $.
    dmncan.2 $e |- H = ( 2nd ` R ) $.
    dmncan.3 $e |- X = ran G $.
    dmncan.4 $e |- Z = ( Id ` G ) $.
    $( Cancellation law for domains. $)
    dmncan1 $p |- ( ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ C e. X ) )
                      /\ A =/= Z ) -> ( ( A H B ) = ( A H C ) -> B = C ) ) $=
      ( cdmn wcel w3a wa wne co cgs cfv wceq cring eqid ringsubdi dmnrng sylan
      adantr eqeq1d wi wo cgr grpdivcl 3expb ringgrp syl adantlr dmnnzd 3exp2
      imp31 syldan exp43 3imp2 neor syl6ib com23 imp sylbird wb grpeqdivid
      ringcl 3adant3r3 3adant3r2 syl3anc 3adantr1 3imtr4d ) DMNZAGNZBGNZCGNZOZP
      ZAHQZPZABFRZACFRZESTZRZHUAZBCWFRZHUAZWDWEUAZBCUAZWCWHAWIFRZHUAZWJWCWMWGHW
      AWMWGUAZWBDUBNZVTWOVPABCWFDEFGIJKWFUCZUDDUEZUFUGUHWAWBWNWJUIWAWNWBWJWAWNA
      HUAWJUJZWBWJUIVPVQVRVSWNWSUIZVPVQVRVSWTVPVQPVRVSPZWIGNZWTVPXAXBVQEUKNZXAX
      BVPXCVRVSXBBCWFEGKWQULUMVPWPXCWRDEIUNUOZUFUPVPVQXBWTVPVQXBWNWSAWIDEFGHIJK
      LUQURUSUTVAVBWJAHVCVDVEVFVGWAWKWHVHZWBXCWDGNZWEGNZXEWAWDWEWFHEGKLWQVIVPXC
      VTXDUGWPVTXFVPWPVQVRXFVSABDEFGIJKVJVKWRUFWPVTXGVPWPVQVSXGVRACDEFGIJKVJVLW
      RUFVMUGWAWLWJVHZWBVPVRVSXHVQXCXAXHVPXCVRVSXHBCWFHEGKLWQVIUMXDUFVNUGVO $.
      $( [25-Jun-2011] $) $( [6-Jan-2011] $)

    $( Cancellation law for domains. $)
    dmncan2 $p |- ( ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ C e. X ) )
                      /\ C =/= Z ) -> ( ( A H C ) = ( B H C ) -> A = B ) ) $=
      ( cdmn wcel w3a wa wne co wceq wb ccring crngcom 3adant3r2 3adant3r1
      eqeq12d dmncrng sylan adantr wi dmncan1 3anrot biimpri sylanl2 sylbid )
      DMNZAGNZBGNZCGNZOZPZCHQZPACFRZBCFRZSZCAFRZCBFRZSZABSZUTVDVGTZVADUANZUSVIU
      OVJUSPVBVEVCVFVJUPURVBVESUQACDEFGIJKUBUCVJUQURVCVFSUPBCDEFGIJKUBUDUEDUFUG
      UHUOURUPUQOZVAVGVHUIUSCABDEFGHIJKLUJVKUSURUPUQUKULUMUN $.
      $( [25-Jun-2011] $) $( [6-Jan-2011] $)
  $}
$( (End of Jeff Madsen's mathbox.) $)


