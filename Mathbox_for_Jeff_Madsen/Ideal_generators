$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ideal generators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c IdlGen $.

  $( Extend class notation with the ideal generation function. $)
  cigen $a class IdlGen $.

  ${
    $d r s i j $.
    $( Define the ideal generated by a subset of a ring. $)
    df-igen $a |- IdlGen = ( r e. RingOps , s e. ~P ran ( 1st ` r ) |->
                      |^| { j e. ( Idl ` r ) | s C_ j } ) $.
  $}

  ${
    $d R r s i j $.  $d S r s i j $.  $d X r s i j $.  $d I j $.
    igenval.1 $e |- G = ( 1st ` R ) $.
    igenval.2 $e |- X = ran G $.
    $( The ideal generated by a subset of a ring.  (Contributed by Jeff Madsen,
       10-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       20-Dec-2013.) $)
    igenval $p |- ( ( R e. RingOps /\ S C_ X ) -> ( R IdlGen S ) =
                                        |^| { j e. ( Idl ` R ) | S C_ j } ) $=
      ( vr vs cring wcel wss cv cidl cfv crab cint cvv wceq c1st cigen co wa c0
      wne wrex rngidl sseq2 rcla4ev sylan rabn0 sylibr intex sylib cpw crn fvex
      eqeltri elpw2 simpl fveq2d wb sseq1 adantl rabeqbidv inteqd fveq2 syl6eqr
      rnex rneqd pweqd df-igen ovmpt2x syl3an2br mpd3an3 ) AJKZBELZBCMZLZCANOZP
      ZQZRKZABUAUBWBSZVPVQUCZWAUDUEZWCWEVSCVTUFZWFVPEVTKVQWGADEFGUGVSVQCEVTVREB
      UHUIUJVSCVTUKULWAUMUNVQVPBEUOZKWCWDBEEDUPZRGDDATOZRFATUQURVIURUSHIABJHMZT
      OZUPZUOIMZVRLZCWKNOZPZQWBUARWHWKASZWNBSZUCZWQWAWTWOVSCWPVTWTWKANWRWSUTVAW
      SWOVSVBWRWNBVRVCVDVEVFWRWMEWRWMWIEWRWLDWRWLWJDWKATVGFVHVJGVHVKCIHVLVMVNVO
      $.
      $( [20-Dec-2013] $) $( [10-Jun-2010] $)

    $( A set is a subset of the ideal it generates.  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    igenss $p |- ( ( R e. RingOps /\ S C_ X ) -> S C_ ( R IdlGen S ) ) $=
      ( vj cring wcel wss wa cigen co cv cidl cfv crab cint ssintub igenval
      sseq2d mpbiri ) AHIBDJKZBABLMZJBBGNJGAOPZQRZJGBUESUCUDUFBABGCDEFTUAUB $.
      $( [10-Jun-2010] $)

    $( The ideal generated by a set is an ideal.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    igenidl $p |- ( ( R e. RingOps /\ S C_ X ) ->
                              ( R IdlGen S ) e. ( Idl ` R ) ) $=
      ( vj cring wcel wss wa cigen co cv cidl cfv crab cint igenval c0 wne wrex
      rngidl sseq2 rcla4ev sylan sylibr ssrab2 intidl mp3an3 syldan eqeltrd
      rabn0 ) AHIZBDJZKZABLMBGNZJZGAOPZQZRZUSABGCDEFSUNUOUTTUAZVAUSIZUPURGUSUBZ
      VBUNDUSIUOVDACDEFUCURUOGDUSUQDBUDUEUFURGUSUMUGUNVBUTUSJVCURGUSUHUTAUIUJUK
      UL $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R j $.  $d S j $.  $d I j $.
    $( The ideal generated by a set is the minimal ideal containing that set.
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    igenmin $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) /\ S C_ I )
                                              -> ( R IdlGen S ) C_ I ) $=
      ( vj cring wcel cidl cfv wss w3a cigen co cv crab cint wceq c1st crn eqid
      wa idlss ancoms igenval sylan2 anassrs syldanl 3impa sseq2 intss1 3adant1
      sstr elrab sylbir eqsstrd ) AEFZCAGHZFZBCIZJABKLZBDMZIZDUPNZOZCUOUQURUSVC
      PZUOUQCAQHZRZIZURVDAVECVFVESZVFSZUAUOVGURVDVGURTUOBVFIZVDURVGVJBCVFUKUBAB
      DVEVFVHVIUCUDUEUFUGUQURVCCIZUOUQURTCVBFVKVAURDCUPUTCBUHULCVBUIUMUJUN $.
      $( [10-Jun-2010] $)

    $( The ideal generated by an ideal is that ideal.  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    igenidl2 $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) )
                                          -> ( R IdlGen I ) = I ) $=
      ( vj cring wcel cidl cfv wa cigen co cv wss crab cint c1st crn wceq idlss
      eqid igenval syldan intmin adantl eqtrd ) ADEZBAFGZEZHABIJZBCKLCUFMNZBUEU
      GBAOGZPZLUHUIQAUJBUKUJSZUKSZRABCUJUKULUMTUAUGUIBQUECBUFUBUCUD $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R i j $.  $d S i j $.  $d I j $.  $d X i $.
    igenval2.1 $e |- G = ( 1st ` R ) $.
    igenval2.2 $e |- X = ran G $.
    $( The ideal generated by a subset of a ring.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    igenval2 $p |- ( ( R e. RingOps /\ S C_ X ) -> ( ( R IdlGen S ) = I <->
                                  ( I e. ( Idl ` R ) /\ S C_ I /\
                          A. j e. ( Idl ` R ) ( S C_ j -> I C_ j ) ) ) ) $=
      ( vi wcel wss wa wceq cv wi wral w3a igenmin adantr sseq2 cring cigen cfv
      co cidl igenidl igenss 3expia ralrimiva 3jca eleq1 sseq1 imbi2d 3anbi123d
      ralbidv syl5ibcom 3adant3r3 adantlr crab cint ralrab biimpri ssint sylibr
      3ad2ant3 adantl igenval sseqtr4d eqssd ex impbid ) AUAJZBFKZLZABUBUDZEMZE
      AUEUCZJZBEKZBCNZKZEVTKZOZCVQPZQZVNVOVQJZBVOKZWAVOVTKZOZCVQPZQVPWEVNWFWGWJ
      ABDFGHUFABDFGHUGVLWJVMVLWICVQVLVTVQJWAWHABVTRUHUISUJVPWFVRWGVSWJWDVOEVQUK
      VOEBTVPWIWCCVQVPWHWBWAVOEVTULUMUOUNUPVNWEVPVNWELZVOEVLWEVOEKZVMVLVRVSWLWD
      ABERUQURWKEBINZKZIVQUSZUTZVOWEEWPKZVNWDVRWQVSWDWBCWOPZWQWRWDWNWAWBCIVQWMV
      TBTVAVBCEWOVCVDVEVFVNVOWPMWEABIDFGHVGSVHVIVJVK $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R j x y u v w r s $.  $d X j x y u v w r s $.  $d G x y r s $.
    $d H j x y u v w r s $.  $d A j x y u v w r s $.
    prnc.1 $e |- G = ( 1st ` R ) $.
    prnc.2 $e |- H = ( 2nd ` R ) $.
    prnc.3 $e |- X = ran G $.
    $( A principal ideal (an ideal generated by one element) in a commutative
       ring.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    prnc $p |- ( ( R e. CRing /\ A e. X ) -> ( R IdlGen { A } ) =
                                  { x e. X | E. y e. X x = ( y H A ) } ) $=
      ( vv vw wcel wa co cv wceq wrex wral opreq1 vj vu vr vs ccring cigen crab
      csn cidl cfv wss wi w3a cgi cring crngrng ssrab2 a1i eqid ring0cl ringolz
      adantr eqcomd eqeq2d rcla4ev syl11anc eqeq1 rexbidv elrab sylanbrc syl6bb
      cbvrexv ringodir 3exp2 imp42 ringgcl 3expib imdistani ringocl 3expa mpan2
      ad2antlr sylan eqeltrrd an32s anassrs opreq2 syl5ibrcom rexlimdva adantld
      eleq1d syl5bi ralrimiv ringoass anass1rs ralrimiva jca ralbidv anbi12d wb
      3jca isidlc mpbird simpr c1st rneqi eqtri ring1cl ringolidm snssd biimpar
      crn snssg idllmulcl eleq1 rabss sylibr syl5 expdimp snssi igenval2 syl2an
      ex ) DUEMZCGMZNZDCUHZUFOAPZBPZCFOZQZBGRZAGUGZQZYMDUIUJZMZYGYMUKZYGUAPZUKZ
      YMYRUKZULZUAYOSZUMZYFYPYQUUBYFYPYMGUKZEUNUJZYMMZUBPZKPZEOZYMMZKYMSZLPZUUG
      FOZYMMZLGSZNZUBYMSZUMZYDDUOMZYEUURDUPZUUSYENZUUDUUFUUQUUDUVAYLAGUQURUVAUU
      EGMZUUEYJQZBGRZUUFUUSUVBYEDEGUUEHJUUEUSZUTVBZUVAUVBUUEUUECFOZQZUVDUVFUVAU
      VGUUECDEFGUUEUVEJHIVAVCUVCUVHBUUEGYIUUEQYJUVGUUEYIUUECFTVDVEVFYLUVDAUUEGY
      HUUEQYKUVCBGYHUUEYJVGVHVIVJUVAUUPUBYMUUGYMMUUGGMZUUGUCPZCFOZQZUCGRZNUVAUU
      PYLUVMAUUGGYHUUGQZYLUUGYJQZBGRUVMUVNYKUVOBGYHUUGYJVGVHUVOUVLBUCGYIUVJQYJU
      VKUUGYIUVJCFTVDVLVKVIUVAUVMUUPUVIUVAUVLUUPUCGUVAUVJGMZNZUUPUVLUVKUUHEOZYM
      MZKYMSZUULUVKFOZYMMZLGSZNUVQUVTUWCUVQUVSKYMUUHYMMUUHGMZUUHUDPZCFOZQZUDGRZ
      NUVQUVSYLUWHAUUHGYHUUHQZYLUUHYJQZBGRUWHUWIYKUWJBGYHUUHYJVGVHUWJUWGBUDGYIU
      WEQYJUWFUUHYIUWECFTVDVLVKVIUVQUWHUVSUWDUVQUWGUVSUDGUVQUWEGMZNUVSUWGUVKUWF
      EOZYMMZUVAUVPUWKUWMUUSUVPUWKNZYEUWMUUSUWNNZYENUVJUWEEOZCFOZUWLYMUUSUVPUWK
      YEUWQUWLQZUUSUVPUWKYEUWRUVJUWECDEFGHIJVMVNVOUWOUUSUWPGMZNZYEUWQYMMZUUSUWN
      UWSUUSUVPUWKUWSUVJUWEDEGHJVPVQVRUWTYENUWQGMZUWQYJQZBGRZUXAUUSUWSYEUXBUWPC
      DEFGHIJVSVTUWSUXDUUSYEUWSUWQUWQQZUXDUWQUSUXCUXEBUWPGYIUWPQYJUWQUWQYIUWPCF
      TVDVEWAWBYLUXDAUWQGYHUWQQYKUXCBGYHUWQYJVGVHVIVJWCWDWEWFUWGUVRUWLYMUUHUWFU
      VKEWGWKWHWIWJWLWMUVQUWBLGUVAUULGMZUVPUWBUVAUXFUVPNZNUULUVJFOZCFOZUWAYMUUS
      UXGYEUXIUWAQZUUSUXFUVPYEUXJUUSUXFUVPYEUXJUULUVJCDEFGHIJWNVNVOWEUUSUXGYEUX
      IYMMZUUSUXGNUUSUXHGMZNZYEUXKUUSUXGUXLUUSUXFUVPUXLUULUVJDEFGHIJVSVQVRUXMYE
      NUXIGMZUXIYJQZBGRZUXKUUSUXLYEUXNUXHCDEFGHIJVSVTUXLUXPUUSYEUXLUXIUXIQZUXPU
      XIUSUXOUXQBUXHGYIUXHQYJUXIUXIYIUXHCFTVDVEWAWBYLUXPAUXIGYHUXIQYKUXOBGYHUXI
      YJVGVHVIVJWCWEWDWOWPWQUVLUUKUVTUUOUWCUVLUUJUVSKYMUVLUUIUVRYMUUGUVKUUHETWK
      WRUVLUUNUWBLGUVLUUMUWAYMUUGUVKUULFWGWKWRWSWHWIWJWLWMXAWCYDYPUURWTYEUBKLDE
      FYMGUUEHIJUVEXBVBXCYFCYMYFYECYJQZBGRZCYMMYDYEXDYDUUSYEUXSUUTUVAFUNUJZGMZC
      UXTCFOZQZUXSUUSUYAYEDUXTFGGEXLDXEUJZXLJEUYDHXFXGZIUXTUSZXHVBUVAUYBCCDUXTF
      GIUYEUYFXIVCUXRUYCBUXTGYIUXTQYJUYBCYIUXTCFTVDVEVFWCYLUXSACGYHCQYKUXRBGYHC
      YJVGVHVIVJXJYDUUSYEUUBUUTUVAUUAUAYOUUSYRYOMZYEUUAUUSUYGNZYEYSYTYEYSNCYRMZ
      UYHYTYEUYIYSCYRGXMXKUYHUYIYTUYHUYINZYLYHYRMZULZAGSYTUYJUYLAGUYJUYLYHGMUYJ
      YKUYKBGUYJYIGMZNUYKYKYJYRMZUYHUYIUYMUYNCYIDEFYRGHIJXNWFYHYJYRXOWHWIVBWPYL
      AGYRXPXQYCXRXSWEWPWCXAYDUUSYGGUKYNUUCWTYEUUTCGXTDYGUAEYMGHJYAYBXC $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d K x y z $.  $d X x y z $.  $d G y z $.  $d H x y z $.  $d U x y z $.
    $d Z x y $.
    isfldidl.1 $e |- G = ( 1st ` K ) $.
    isfldidl.2 $e |- H = ( 2nd ` K ) $.
    isfldidl.3 $e |- X = ran G $.
    isfldidl.4 $e |- Z = ( Id ` G ) $.
    isfldidl.5 $e |- U = ( Id ` H ) $.
    $( Determine if a ring is a field based on its ideals.  (Contributed by
       Jeff Madsen, 10-Jun-2010.) $)
    isfldidl $p |- ( K e. Fld <-> ( K e. CRing /\
                                U =/= Z /\ ( Idl ` K ) = { { Z } , X } ) ) $=
      ( vy vx vz wcel wceq syl cv wrex wa cfld wne cidl cfv csn cpr w3a fldcrng
      ccring cdrng flddivrng divrngidl 3jca cring co cdif wral crngrng 3ad2ant1
      dvrunz simp2 crab crn c1st rneqi eqtri ring1cl ad2antrr cigen eldif snssi
      wn wss igenss sylan2 visset snss biimpri eleq2 syl5ibcom con3d imp anasss
      sylan sylan2b adantlr eldifi snssd igenidl an32s oprex elpr sylib ord mpd
      wo sylanl1 prnc eqtr3d eleqtrd eqeq1 rexbidv elrab simprd eqcom ralrimiva
      rexbii sylibr 3adant2 jca32 isdivrng3 simp1 isfld2 sylanbrc impbii ) DUAO
      ZDUIOZAFUBZDUCUDZFUEZEUFZPZUGZXPXQXRYBDUHXPDUJOZXRDUKZDABCEFGHIJKUTQXPYDY
      BYEDBCEFGHIJULQUMYCYDXQXPYCDUNOZXRLRMRZCUOZAPZLESZMEXTUPZUQZTTYDYCYFXRYLX
      QXRYFYBDURZUSXQXRYBVAXQYBYLXRXQYBTZYJMYKYNYGYKOZTZAYHPZLESZYJYPAEOZYRYPAN
      RZYHPZLESZNEVBZOYSYRTYPAEUUCXQYSYBYOXQYFYSYMDACEEBVCDVDUDZVCIBUUDGVEVFHKV
      GQVHYPDYGUEZVIUOZEUUCXQYFYBYOUUFEPZYMYFYBTYOTZUUFXTPZVLZUUGYFYOUUJYBYOYFY
      GEOZYGXTOZVLZTUUJYGEXTVJYFUUKUUMUUJYFUUKTUUEUUFVMZUUMUUJUUKYFUUEEVMZUUNYG
      EVKDUUEBEGIVNVOUUNUUMUUJUUNUUIUULUUNYGUUFOZUUIUULUUPUUNYGUUFMVPVQVRUUFXTY
      GVSVTWAWBWDWCWEWFUUHUUIUUGUUHUUFYAOZUUIUUGWPYFYOYBUUQYFYOTZYBUUQUURUUFXSO
      ZYBUUQYOYFUUOUUSYOYGEYGEXTWGZWHDUUEBEGIWIVOXSYAUUFVSVTWBWJUUFXTEDUUEVIWKW
      LWMWNWOWQXQYOUUFUUCPZYBYOXQUUKUVAUUTNLYGDBCEGHIWRVOWFWSWTUUBYRNAEYTAPUUAY
      QLEYTAYHXAXBXCWMXDYIYQLEYHAXEXGXHXFXIXJMLDABCEFGHJIKXKXHXQXRYBXLDXMXNXO
      $.
      $( [10-Jun-2010] $)
  $}

  ${
    isfldidl2.1 $e |- G = ( 1st ` K ) $.
    isfldidl2.2 $e |- H = ( 2nd ` K ) $.
    isfldidl2.3 $e |- X = ran G $.
    isfldidl2.4 $e |- Z = ( Id ` G ) $.
    $( Determine if a ring is a field based on its ideals.  (Contributed by
       Jeff Madsen, 6-Jan-2011.) $)
    isfldidl2 $p |- ( K e. Fld <-> ( K e. CRing /\
                              X =/= { Z } /\ ( Idl ` K ) = { { Z } , X } ) ) $=
      ( cfld wcel ccring cgi cfv wne cidl wceq w3a wa 3anass csn cpr eqid cring
      isfldidl wb crngrng eqcom 0ring syl5bb syl necon3bid anbi1d pm5.32i bitri
      3bitr4i ) CJKCLKZBMNZEOZCPNEUAZDUBQZRZUQDUTOZVARZURABCDEFGHIURUCZUEUQUSVA
      SZSUQVCVASZSVBVDUQVFVGUQUSVCVAUQUREDUTUQCUDKZUREQZDUTQZUFCUGVIEURQVHVJURE
      UHCURABDEFGHIVEUIUJUKULUMUNUQUSVATUQVCVATUPUO $.
      $( [6-Jan-2011] $)
  $}

  ${
    $d R a b x y r s $.  $d P a b x y r s $.  $d X a b x y r s $.
    $d G x y r s $.  $d H a b x y r s $.
    ispridlc.1 $e |- G = ( 1st ` R ) $.
    ispridlc.2 $e |- H = ( 2nd ` R ) $.
    ispridlc.3 $e |- X = ran G $.
    $( The predicate "is a prime ideal".  Alternate definition for commutative
       rings.  (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    ispridlc $p |- ( R e. CRing -> ( P e. ( PrIdl ` R ) <-> ( P e. ( Idl ` R )
                              /\ P =/= X /\ A. a e. X A. b e. X
                      ( ( a H b ) e. P -> ( a e. P \/ b e. P ) ) ) ) ) $=
      ( vx vy vs vr wcel co wi wral wss wa ccring cpridl cfv cidl wne cv wo w3a
      cring wb crngrng ispridl syl csn cigen snssi igenidl adantrr adantrl wceq
      syl2an raleq sseq1 orbi1d imbi12d ralbidv orbi2d rcla42v syl11anc adantlr
      wrex crab cab df-rab syl6eq abeq2d anbi12d adantr reeanv anbi2i an4 bitri
      prnc crngm4 3com23 3expa adantllr ringocl syl3an1 3expb idllmulcl sylanl1
      anassrs syldan opreq12 eleq1d syl5ibrcom rexlimdvv adantld syl5bir sylbid
      eqeltrrd ralrimivv igenss visset snss sylibr ssel syl5com orim12d imim12d
      ex syld ralrimdvva adantrd imdistand df-3an 3imtr4g ispridl2 impbid ) BUA
      OZABUBUCOZABUDUCZOZAEUEZFUFZGUFZDPZAOZYFAOZYGAOZUGZQZGERFERZUHZYAYBYDYEKU
      FZLUFZDPZAOZLMUFZRZKNUFZRZUUBASZYTASZUGZQZMYCRNYCRZUHZYOYABUIOZYBUUIUJBUK
      ZKLABCDENMHIJULUMYAYDYETZUUHTUULYNTUUIYOYAUULUUHYNYAYDUUHYNQZYEYAYDUUMYAY
      DTZUUHYMFGEEUUNYFEOZYGEOZTZTZUUHYSLBYGUNZUOPZRZKBYFUNZUOPZRZUVCASZUUTASZU
      GZQZYMYAUUQUUHUVHQZYDYAUUQTZUVCYCOZUUTYCOZUVIYAUUOUVKUUPYAUUJUVBESZUVKUUO
      UUKYFEUPZBUVBCEHJUQVAURYAUUPUVLUUOYAUUJUUSESZUVLUUPUUKYGEUPZBUUSCEHJUQVAU
      SUUGUVHUUAKUVCRZUVEUUEUGZQNMUVCUUTYCYCUUBUVCUTZUUCUVQUUFUVRUUAKUUBUVCVBUV
      SUUDUVEUUEUUBUVCAVCVDVEYTUUTUTZUVQUVDUVRUVGUVTUUAUVAKUVCYSLYTUUTVBVFUVTUU
      EUVFUVEYTUUTAVCVGVEVHVIVJUURYIUVDUVGYLUURYIUVDUURYITZYSKLUVCUUTUWAYPUVCOZ
      YQUUTOZTZYPEOZYPUUBYFDPZUTZNEVKZTZYQEOZYQYTYGDPZUTZMEVKZTZTZYSUURUWDUWOUJ
      ZYIYAUUQUWPYDUVJUWBUWIUWCUWNYAUUOUWBUWIUJUUPYAUUOTZUWIKUVCUWQUVCUWHKEVLUW
      IKVMKNYFBCDEHIJWCUWHKEVNVOVPURYAUUPUWCUWNUJUUOYAUUPTZUWNLUUTUWRUUTUWMLEVL
      UWNLVMLMYGBCDEHIJWCUWMLEVNVOVPUSVQVJVRUWOUWEUWJTZUWGUWLTZMEVKNEVKZTZUWAYS
      UXBUWSUWHUWMTZTUWOUXAUXCUWSUWGUWLNMEEVSVTUWEUWJUWHUWMWAWBUWAUXAYSUWSUWAUW
      TYSNMEEUWAUUBEOZYTEOZTZUWTYSQUWAUXFTZYSUWTUWFUWKDPZAOUXGUUBYTDPZYHDPZUXHA
      UURUXFUXJUXHUTZYIYAUUQUXFUXKYDYAUUQUXFUXKYAUXFUUQUXKUUBYTYFYGBCDEHIJWDWEW
      FWGVJUUNYIUXFUXJAOZUUQUUNYITUXFUXIEOZUXLUUNUXFUXMYIYAUXFUXMYDYAUXDUXEUXMY
      AUUJUXDUXEUXMUUKUUBYTBCDEHIJWHWIWJVJVJUUNYIUXMUXLYAUUJYDYIUXMTUXLUUKYHUXI
      BCDAEHIJWKWLWMWNWGXBUWTYRUXHAYPUWFYQUWKDWOWPWQXLWRWSWTXAXCXLYAUUQUVGYLQYD
      UVJUVEYJUVFYKUVJYFUVCOZUVEYJYAUUOUXNUUPUWQUVBUVCSZUXNYAUUJUVMUXOUUOUUKUVN
      BUVBCEHJXDVAYFUVCFXEXFXGURUVCAYFXHXIUVJYGUUTOZUVFYKYAUUPUXPUUOUWRUUSUUTSZ
      UXPYAUUJUVOUXQUUPUUKUVPBUUSCEHJXDVAYGUUTGXEXFXGUSUUTAYGXHXIXJVJXKXMXNXLXO
      XPYDYEUUHXQYDYEYNXQXRXAYAUUJYOYBQUUKUUJYOYBABCDEFGHIJXSXLUMXT $.
      $( [19-Jun-2010] $)

    $d A a b $.  $d B b $.
    $( Property of a prime ideal in a commutative ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    pridlc $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
          ( A e. X /\ B e. X /\ ( A H B ) e. P ) ) -> ( A e. P \/ B e. P ) ) $=
      ( va vb wcel cfv wa cv co wo wi wral ccring cpridl w3a cidl biimpa simp3d
      wne ispridlc wceq opreq1 eleq1d eleq1 orbi1d imbi12d opreq2 rcla42v com12
      orbi2d exp3a 3imp2 sylan ) DUAMZCDUBNMZOZKPZLPZFQZCMZVECMZVFCMZRZSZLGTKGT
      ZAGMZBGMZABFQZCMZUCACMZBCMZRZVDCDUDNMZCGUGZVMVBVCWAWBVMUCCDEFGKLHIJUHUEUF
      VMVNVOVQVTVMVNVOVQVTSZVNVOOVMWCVLWCAVFFQZCMZVRVJRZSKLABGGVEAUIZVHWEVKWFWG
      VGWDCVEAVFFUJUKWGVIVRVJVEACULUMUNVFBUIZWEVQWFVTWHWDVPCVFBAFUOUKWHVJVSVRVF
      BCULURUNUPUQUSUTVA $.
      $( [17-Jun-2011] $)

    $( Property of a prime ideal in a commutative ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    pridlc2 $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
            ( A e. ( X \ P ) /\ B e. X /\ ( A H B ) e. P ) ) -> B e. P ) $=
      ( ccring wcel cpridl cfv wa cdif co w3a wn eldifn adantl wi eldifi pridlc
      3ad2ant1 ord syl3anr1 mpd ) DKLCDMNLOZAGCPLZBGLZABFQCLZRZOACLZSZBCLZUMUOU
      IUJUKUOULAGCTUEUAUJAGLZUIUKULUOUPUBAGCUCUIUQUKULROUNUPABCDEFGHIJUDUFUGUH
      $.
      $( [17-Jun-2011] $)

    $( Property of a prime ideal in a commutative ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    pridlc3 $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
          ( A e. ( X \ P ) /\ B e. ( X \ P ) ) ) -> ( A H B ) e. ( X \ P ) ) $=
      ( ccring wcel cpridl cfv wa cdif co wn eldifi wi crngrng anim12i ad2antll
      cring ringocl 3expb syl2an adantlr eldifn pridlc2 3exp2 imp32 sylanr2 mpd
      con3d eldif sylanbrc ) DKLZCDMNLZOZAGCPZLZBVALZOZOZABFQZGLZVFCLZRZVFVALUR
      VDVGUSURDUDLZAGLZBGLZOVGVDDUAVBVKVCVLAGCSBGCSZUBVJVKVLVGABDEFGHIJUEUFUGUH
      VEBCLZRZVIVCVOUTVBBGCUIUCVCUTVBVLVOVITVMUTVBVLOOVHVNUTVBVLVHVNTUTVBVLVHVN
      ABCDEFGHIJUJUKULUOUMUNVFGCUPUQ $.
      $( [17-Jun-2011] $)
  $}

  ${
    $d R a b $.  $d Z a b $.  $d H a b $.  $d X a b $.
    isdmn3.1 $e |- G = ( 1st ` R ) $.
    isdmn3.2 $e |- H = ( 2nd ` R ) $.
    isdmn3.3 $e |- X = ran G $.
    isdmn3.4 $e |- Z = ( Id ` G ) $.
    isdmn3.5 $e |- U = ( Id ` H ) $.
    $( The predicate "is a domain", alternate expression.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    isdmn3 $p |- ( R e. Dmn <-> ( R e. CRing /\ U =/= Z /\ A. a e. X A. b e. X
                                ( ( a H b ) = Z -> ( a = Z \/ b = Z ) ) ) ) $=
      ( wcel wa wceq wi wral cfv syl cdmn cprrng ccring wne cv co wo w3a isdmn2
      cring csn cpridl isprrng cidl ispridlc crngrng biantrurd 3anass 0idl c1st
      crn rneqi eqtri ring1cl eleq2 elsni syl6bir syl5com c1o cen uznzr ring0cl
      wb en1eqsn eqcomd ex sylbird impbid necon3bid oprex elsnc orbi12i imbi12i
      wbr elsn a1i 2ralbidv anbi12d bitr3d syl5bb 3bitr3d pm5.32i ancom 3bitr4i
      bitri ) AUANAUBNZAUCNZOZWQBFUDZGUEZHUEZDUFZFPZWTFPZXAFPZUGZQZHERGERZUHZAU
      IWQWPOWQWSXHOZOWRXIWQWPXJWPAUJNZFUKZAULSNZOZWQXJACFILUMWQXMXLAUNSNZXLEUDZ
      XBXLNZWTXLNZXAXLNZUGZQZHERGERZUHZXNXJXLACDEGHIJKUOWQXKXMAUPZUQYCXOXPYBOZO
      ZWQXJXOXPYBURWQYEYFXJWQXOYEWQXKXOYDACFILUSTUQWQXPWSYBXHWQXLEBFWQXKXLEPZBF
      PZVMYDXKYGYHXKBENZYGYHABDEECVAAUTSZVAKCYJIVBVCJMVDYGYIBXLNYHXLEBVEBFVFVGV
      HXKYHEVIVJWDZYGABCDEFIJLMKVKXKFENZYKYGQACEFIKLVLYLYKYGYLYKOEXLFEVNVOVPTVQ
      VRTVSWQYAXGGHEEYAXGVMWQXQXCXTXFXBFWTXADVTWAXRXDXSXEGFWEHFWEWBWCWFWGWHWIWJ
      WKWJWLWPWQWMWQWSXHURWNWO $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R a b $.  $d H a b $.  $d X a b $.  $d Z a b $.  $d A a b $.  $d B b $.
    dmnnzd.1 $e |- G = ( 1st ` R ) $.
    dmnnzd.2 $e |- H = ( 2nd ` R ) $.
    dmnnzd.3 $e |- X = ran G $.
    dmnnzd.4 $e |- Z = ( Id ` G ) $.
    $( A domain has no zero-divisors (besides zero).  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    dmnnzd $p |- ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ ( A H B ) = Z ) )
                                                  -> ( A = Z \/ B = Z ) ) $=
      ( va vb wcel co wceq wo wi cv wral cdmn wa ccring cgi cfv wne eqid isdmn3
      simp3bi opreq1 eqeq1d eqeq1 orbi1d imbi12d opreq2 orbi2d rcla42v syl5com
      exp3a 3imp2 ) CUANZAFNZBFNZABEOZGPZAGPZBGPZQZVAVBVCVEVHRZVALSZMSZEOZGPZVJ
      GPZVKGPZQZRZMFTLFTZVBVCUBVIVACUCNEUDUEZGUFVRCVSDEFGLMHIJKVSUGUHUIVQVIAVKE
      OZGPZVFVOQZRLMABFFVJAPZVMWAVPWBWCVLVTGVJAVKEUJUKWCVNVFVOVJAGULUMUNVKBPZWA
      VEWBVHWDVTVDGVKBAEUOUKWDVOVGVFVKBGULUPUNUQURUSUT $.
      $( [19-Jun-2010] $)
  $}

  ${
    dmncan.1 $e |- G = ( 1st ` R ) $.
    dmncan.2 $e |- H = ( 2nd ` R ) $.
    dmncan.3 $e |- X = ran G $.
    dmncan.4 $e |- Z = ( Id ` G ) $.
    $( Cancellation law for domains.  (Contributed by Jeff Madsen,
       6-Jan-2011.) $)
    dmncan1 $p |- ( ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ C e. X ) )
                      /\ A =/= Z ) -> ( ( A H B ) = ( A H C ) -> B = C ) ) $=
      ( wcel wa co wceq sylan adantr wi 3expb cdmn w3a wne cgs cfv cring dmnrng
      eqid ringsubdi eqeq1d wo cgr ringogrpo syl grpodivcl adantlr dmnnzd 3exp2
      imp31 syldan exp43 3imp2 neor syl6ib com23 sylbird wb 3adant3r3 3adant3r2
      imp ringocl grpoeqdivid syl111anc 3adantr1 3imtr4d ) DUAMZAGMZBGMZCGMZUBZ
      NZAHUCZNZABFOZACFOZEUDUEZOZHPZBCWFOZHPZWDWEPZBCPZWCWHAWIFOZHPZWJWCWMWGHWA
      WMWGPZWBVPDUFMZVTWODUGZABCWFDEFGIJKWFUHZUIQRUJWAWBWNWJSWAWNWBWJWAWNAHPWJU
      KZWBWJSVPVQVRVSWNWSSZVPVQVRVSWTVPVQNVRVSNZWIGMZWTVPXAXBVQVPEULMZXAXBVPWPX
      CWQDEIUMUNZXCVRVSXBBCWFEGKWRUOTQUPVPVQXBWTVPVQXBWNWSAWIDEFGHIJKLUQURUSUTV
      AVBWJAHVCVDVEVJVFWAWKWHVGZWBWAXCWDGMZWEGMZXEVPXCVTXDRVPWPVTXFWQWPVQVRXFVS
      ABDEFGIJKVKVHQVPWPVTXGWQWPVQVSXGVRACDEFGIJKVKVIQWDWEWFHEGKLWRVLVMRWAWLWJV
      GZWBVPVRVSXHVQVPXCXAXHXDXCVRVSXHBCWFHEGKLWRVLTQVNRVO $.
      $( [6-Jan-2011] $)

    $( Cancellation law for domains.  (Contributed by Jeff Madsen,
       6-Jan-2011.) $)
    dmncan2 $p |- ( ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ C e. X ) )
                      /\ C =/= Z ) -> ( ( A H C ) = ( B H C ) -> A = B ) ) $=
      ( cdmn wcel w3a wa wne co wceq crngcom ccring dmncrng 3adant3r2 3adant3r1
      wb eqeq12d sylan adantr wi 3anrot biimpri dmncan1 sylanl2 sylbid ) DMNZAG
      NZBGNZCGNZOZPZCHQZPACFRZBCFRZSZCAFRZCBFRZSZABSZUTVDVGUEZVAUODUANZUSVIDUBV
      JUSPVBVEVCVFVJUPURVBVESUQACDEFGIJKTUCVJUQURVCVFSUPBCDEFGIJKTUDUFUGUHUSUOU
      RUPUQOZVAVGVHUIVKUSURUPUQUJUKCABDEFGHIJKLULUMUN $.
      $( [6-Jan-2011] $)
  $}
$( (End of Jeff Madsen's mathbox.) $)


