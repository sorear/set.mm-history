$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intervals (continued)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d R k u v x y $.  $d F k u v $.
    ismrer1.1 $e |- R = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    ismrer1.2 $e |- F = { <. x , y >. | ( x e. RR /\
                                              y = ( { 1 } X. { x } ) ) } $.
    $( An isometry between ` RR ` and ` RR ^ 1 ` .  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    ismrer1 $p |- F e. ( R Ismty ( Rn ` 1 ) ) $=
      ( vu vv c1 cfv co wcel cr csn cv wceq ax-mp c2 cexp csqr crrn cismty cmap
      vk wf1o cme wa wb remet cn 1nn rrnmet remetba cdm cfz rrndm cz 1z opreq2i
      wral fzsn eqtr2i isismty wfn crn wi dff1o6 cxp snex xpex fnopab2 wrex cab
      mp2an cop cc ax-1cn elisseti visset xpsn eqeq2i abbii copab rneqi rnopab2
      rexbii eqtri reex mapsn 3eqtr4i sneq xpeq2 syl fvopab4 eqeqan12d wne snnz
      c0 xp11 sneqr adantl sylbi syl6bi rgen2a mpbir3an cmin cabs csu cvv oprex
      opreq12d opreq1d fsum1i fveq2i a1i fveq1d snid fvconst2 syl6eq opreqan12d
      fveq2 fveq2d resubcl absresq eqcomd cc0 cle wbr recn abscl sqrsq syl11anc
      absge0 eqtrd 3eqtrrd remetdval wf f1of ffvelrni syl6eleqr rrnmval 3eqtr4d
      adantr syl111anc mpbir2an ) DCIUAJZUBKLZMMINZUCKZDUEZGOZHOZCKZUUKDJZUULDJ
      ZUUFKZPZHMUTGMUTZCUFLUUFUFLZUUGUUJUURUGUHCEUIIUJLZUUSUKIULQGHDCUUFMUUICEU
      MUUFUNUNZMIIUOKZUCKZUUIUUTUVAUVCPUKIUPQUVBUUHMUCIUQLZUVBUUHPURIVAQUSZVBVC
      VNUUJDMVDDVEZUUIPUUNUUOPZUUKUULPZVFZHMUTGMUTGHMUUIDVGABMUUHAOZNZVHZDUUHUV
      KIVIZUVJVIVJFVKBOZUVLPZAMVLZBVMZUVNIUVJVONZPZAMVLZBVMUVFUUIUVPUVTBUVOUVSA
      MUVLUVRUVNIUVJIVPVQVRZAVSVTWAWFWBUVFUVJMLUVOUGABWCZVEUVQDUWBFWDABMUVLWEWG
      AMIBWHUWAWIWJUVIGHMUUKMLZUULMLZUGZUVGUUHUUKNZVHZUUHUULNZVHZPZUVHUWCUWDUUN
      UWGUUOUWIABUUKUVLUWGMDUVJUUKPUVKUWFPUVLUWGPUVJUUKWKUVKUWFUUHWLWMFUUHUWFUV
      MUUKVIVJWNZABUULUVLUWIMDUVJUULPUVKUWHPUVLUWIPUVJUULWKUVKUWHUUHWLWMFUUHUWH
      UVMUULVIVJWNZWOUWJUUHUUHPZUWFUWHPZUGZUVHUUHWRWPUWFWRWPUWJUWOUHIUWAWQUUKGV
      SZWQUUHUWFUUHUWHWSVNUWNUVHUWMUUKUULUWPWTXAXBXCXDXEZUUQGHMUWEUUKUULXFKZXGJ
      ZUVBUDOZUUNJZUWTUUOJZXFKZRSKZUDXHZTJZUUMUUPUWEUXFIUUNJZIUUOJZXFKZRSKZTJZU
      WRRSKZTJZUWSUXFUXKPUWEUXEUXJTUXJXILUVDUXEUXJPUXIRSXJURUXDUXJXIUDIUWTIPZUX
      CUXIRSUXNUXAUXGUXBUXHXFUWTIUUNYAUWTIUUOYAXKXLXMVNXNXOUWEUXJUXLTUWEUXIUWRR
      SUWCUWDUXGUUKUXHUULXFUWCUXGIUWGJZUUKUWCIUUNUWGUWKXPIUUHLZUXOUUKPIUWAXQZUU
      HUUKIUWPXRQXSUWDUXHIUWIJZUULUWDIUUOUWIUWLXPUXPUXRUULPUXQUUHUULIHVSXRQXSXT
      XLYBUWEUWRMLZUXMUWSPUUKUULYCUXSUXMUWSRSKZTJZUWSUXSUXLUXTTUXSUXTUXLUWRYDYE
      YBUXSUWSMLZYFUWSYGYHZUYAUWSPUXSUWRVPLZUYBUWRYIZUWRYJWMUXSUYDUYCUYEUWRYMWM
      UWSYKYLYNWMYOUUKUULCEYPUWEUUTUUNUVCLZUUOUVCLZUUPUXFPUUTUWEUKXOUWCUYFUWDUW
      CUUNUUIUVCMUUIUUKDUUJMUUIDYQUWQMUUIDYRQZYSUVEYTUUCUWDUYGUWCUWDUUOUUIUVCMU
      UIUULDUYHYSUVEYTXAUDIUUNUUOUUAUUDUUBXDUUE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R x y f $.  $d T f $.  $d Y f $.
    reheibor.1 $e |- R = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    reheibor.2 $e |- M = ( R |` ( Y X. Y ) ) $.
    reheibor.3 $e |- T = ( MetOpen ` M ) $.
    reheibor.4 $e |- U = ( MetOpen ` R ) $.
    $( Heine-Borel theorem for real numbers.  A subset of ` RR ` is compact iff
       it is closed and bounded.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    reheibor $p |- ( Y C_ RR -> ( T e. Comp <->
                                  ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( vx vy cr c1 cfv wcel co eqid ax-mp a1i cme vf wss crrn cv csn cxp copab
      wceq wa cima cres copn ccomp ccld cbnd cn cfz cmap wb 1nn imassrn fconstg
      crn wf snssi syl11anc reex snex elmap sylibr cz 1z fzsn opreq2i syl6eleqr
      fss fopab frn sstri rrnheibor sylancr ctop chomeosm metres eqeltri opntop
      remet rrnmet pm3.2i cismty ismrer1 id remetba ismtyres syl22anc ismtyhmeo
      mp2an syl chomeo wbr wex cvv opabex2 resexg eleq1 cla4ev hmph syl5ibr imp
      wi comptoppr 3expa syldan cuni uniopn2 hmeocld ismtybnd syl111anc anbi12d
      3bitr4d ) ELUBZMUCNZJUDZLOZKUDMUEZYCUEZUFZUHUIJKUGZEUJZYIUFUKZULNZUMOZYIY
      BULNZUNNOZYJUOOZUIZBUMOZECUNNOZDUOOZUIYAMUPOZYILMMUQPZURPZUBZYLYPUSUTUUCY
      AYIYHVCZUUBYHEVALUUBYHVDUUDUUBUBJKLUUBYGYHYHQZYDYGLYEURPZUUBYDYELYGVDZYGU
      UFOYDYEYFYGVDYFLUBUUGYEYCLVBYCLVEYEYFLYGVPVFLYEYGVGMVHVIVJUUAYELURMVKOUUA
      YEUHVLMVMRVNVOVQLUUBYHVRRVSSYKYMYJMUUBYIUUBQYJQZYKQZYMQZVTWAYABWBOZYKWBOZ
      UIZYHEUKZBYKWCPZOZYQYLUSZUUKUULDTOZUUKDAEEUFUKZTGATOZUUSTOAFWGZAEWDRWEZDB
      HWFRYJTOZUULYBTOZUVCYTUVDUTMWHRZYBYIWDRZYJYKUUIWFRWIYAUUNDYJWJPOZUUPYAUUT
      UVDYHAYBWJPOZYAUVGUUTYAUVASUVDYAUVESUVHYAJKAYHFUUEWKZSYAWLZEYIDYJYHAYBLAF
      WMZYIQGUUHWNWOZUURUVCUVGUUPXJUVBUVFUUNBYKDYJHUUIWPWQWRUUMUUPBYKWSWTZUUQUU
      MUUPUVMUUPUVMUUMUAUDZUUOOZUAXAUVOUUPUAUUNYHXBOUUNXBOJKLYGVGXCYHEXBXDRUVNU
      UNUUOXEXFUABYKXGXHXIUUKUULUVMUUQBYKXKXLXMWAYAYRYNYSYOYACWBOZYMWBOZYHCYMWC
      POZYAYRYNUSUVPYAUUTUVPUVAACIWFRSUVQYAUVDUVQUVEYBYMUUJWFRSUVRYAUVHUVRUVIUU
      TUVDUVHUVRXJUVAUVEYHCYMAYBIUUJWPWQRSUVJEYHCYMLUUTLCXNUHUVAACLUVKIXORXPWOY
      AUURUVCUVGYSYOUSUURYAUVBSUVCYAUVFSUVLUUNDYJXQXRXSXT $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d J x y z $.  $d M x y z $.
    iccbnd.1 $e |- J = ( A [,] B ) $.
    iccbnd.2 $e |- M = ( ( abs o. - ) |` ( J X. J ) ) $.
    $( A closed interval in ` RR ` is bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccbnd $p |- ( ( A e. RR /\ B e. RR ) -> M e. Bnd ) $=
      ( cr wcel wa co wceq cc cle wbr c1 caddc clt recn wb syl11anc vx vy vz cv
      cdm cbl cfv crp wrex wral cbnd cicc cabs cmin ccom cxp cres cme wss cnmet
      eqid iccssre ax-resscn syl6ss cnmetba sylancr xpeq12i reseq2i eqtri dmeqi
      metssba2 syl6eqr eleq2d w3a elicc2 cc0 resubcl abscl 3syl readdcl sylancl
      ancoms 1re a1i absge0 lt01 addgegt0 syl22anc sylanbrc adantr cin 3ad2ant1
      elrp ad2antlr ad2antrr syl111anc sseld adantlr cnmetdval syl2an 3ad2antr1
      elbl imp 3ad2antl1 adantll cneg absle simplr2 simpr3 simpr1 simpllr leneg
      mpbid wi simplll renegcl syl simplr1 le2add mp2and ad3antrrr adantl negcl
      addcom negsubdi eqtr4d negsub 3brtr3d simpr2 simpll simp1 mpbir2and leabs
      simplr3 letrd ltp1 lelttrd ex sylbid ax-mp eqbrtrd ssrdv sseqin2 syl6reqr
      ineq2i 3eqtr4rd syl5eqss biimpar syl6eleqr opreq2 eqeq2d rcla4ev ralrimiv
      sylib blssp sylbird metres eqeltri isbnd2 sylibr ) AGHZBGHZIZDUEZUEZUAUDZ
      UBUDZDUFUGZJZKZUBUHUIZUAUVEUJZDUKHZUVCUVKUAUVEUVCUVFUVEHUVFABULJZHZUVKUVC
      UVNUVEUVFUVCUVNUMUNUOZUVNUVNUPZUQZUEZUEZUVEUVCUVPURHZUVNLUSUVNUVTKUVPUVPV
      AZUTZUVCUVNGLABVBVCVDZUVPLUVNUVPUWBVEZVKVFZUVDUVSDUVRDUVPCCUPZUQZUVRFUWGU
      VQUVPCUVNCUVNEEVGVHVIVJVJZVLVMUVCUVOUVFGHZAUVFMNZUVFBMNZVNZUVKABUVFVOZUVC
      UWMUVKUVCUWMIZBAUNJZUMUGZOPJZUHHZUVEUVFUWRUVHJZKZUVKUVCUWSUWMUVCUWRGHZVPU
      WRQNZUWSUVCUWQGHZOGHZUXBUVCUWPGHZUWPLHZUXDUVBUVAUXFBAVQWBZUWPRZUWPVRVSZWC
      UWQOVTWAZUVCUXDUXEVPUWQMNZVPOQNZUXCUXJUXEUVCWCWDUVCUXFUXGUXLUXHUXIUWPWEVS
      UXMUVCWFWDUWQOWGWHUWRWMWIZWJZUWOUVEUVFUWRUVPUFUGJZCWKZUWTUWOUXPUVNWKZUVNU
      XQUVEUWOUVNUXPUSUXRUVNKUWOUCUVNUXPUWOUCUDZUVNHZUXSUXPHZUWOUXTIZUYAUXSLHZU
      VFUXSUVPJZUWRQNZUYBUWAUVFLHZUWSUYAUYCUYEISUWAUYBUWCWDUWMUYFUVCUXTUWJUWKUY
      FUWLUVFRZWLZWNUVCUWSUWMUXTUXNWOUXSUVPUVFUWRLUWEXBWPUVCUXTUYCUWMUVCUXTUYCU
      VCUVNLUXSUWDWQXCWRUWOUXTUYEUWOUXTUXSGHZAUXSMNZUXSBMNZVNZUYEUVCUXTUYLSUWMA
      BUXSVOWJUWOUYLUYEUWOUYLIZUYDUVFUXSUNJZUMUGZUWRQUWMUYLUYDUYOKZUVCUWJUWKUYL
      UYPUWLUWJUYJUYIUYPUYKUWJUYFUYCUYPUYIUYGUXSRZUVFUXSUVPUWBWSWTXAXDXEUYMUYOU
      WQUWRUWMUYLUYOGHZUVCUWJUWKUYLUYRUWLUWJUYJUYIUYRUYKUWJUYIIUYNGHZUYNLHUYRUV
      FUXSVQZUYNRUYNVRVSXAXDXEZUVCUXDUWMUYLUXJWOZUVCUXBUWMUYLUXKWOUYMUYOUWPUWQV
      UAUVCUXFUWMUYLUXHWOZVUBUYMUYOUWPMNZUWPXFZUYNMNZUYNUWPMNZUYMUYSUXFVUDVUFVU
      GISUWMUYLUYSUVCUWJUWKUYLUYSUWLUWJUYJUYIUYSUYKUYTXAXDXEVUCUYNUWPXGTUYMABXF
      ZPJZUVFUXSXFZPJZVUEUYNMUYMUWKVUHVUJMNZVUIVUKMNZUWJUWKUWLUVCUYLXHUYMUYKVUL
      UWOUYIUYJUYKXIUYMUYIUVBUYKVULSUWOUYIUYJUYKXJZUVAUVBUWMUYLXKZUXSBXLTXMUYMU
      VAVUHGHZUWJVUJGHZUWKVULIVUMXNUVAUVBUWMUYLXOUYMUVBVUPVUOBXPXQUWJUWKUWLUVCU
      YLXRZUYMUYIVUQVUNUXSXPXQZAVUHUVFVUJXSWHXTUYMVUIVUHAPJZVUEUYMALHZVUHLHZVUI
      VUTKUVAVVAUVBUWMUYLARYAZUYMBLHZVVBUVCVVDUWMUYLUVBVVDUVABRYBWOZBYCXQAVUHYD
      TUYMVVDVVAVUEVUTKVVEVVCBAYETYFUYMUYFUYCVUKUYNKUWMUYFUVCUYLUYHWNUYLUYCUWOU
      YIUYJUYCUYKUYQWLYBUVFUXSYGTZYHUYMVUKBAXFZPJZUYNUWPMUYMUWLVUJVVGMNZVUKVVHM
      NZUWJUWKUWLUVCUYLYNUYMUYJVVIUWOUYIUYJUYKYIUWOUVAUYIUYJVVISUYLUVAUVBUWMYJU
      YIUYJUYKYKAUXSXLWTXMUYMUWJVUQUVBVVGGHZUWLVVIIVVJXNVURVUSVUOUVAVVKUVBUWMUY
      LAXPYAUVFVUJBVVGXSWHXTVVFUYMVVDVVAVVHUWPKVVEVVCBAYGTYHYLUYMUXFUWPUWQMNVUC
      UWPYMXQYOUYMUXDUWQUWRQNVUBUWQYPXQYQUUAYRYSXCYLYRUUBUVNUXPUUCUUNUXQUXRKUWO
      CUVNUXPEUUEWDUVCUVEUVNKUWMUVCUVNUVTUVEUWFUWIUUDWJUUFUWOUWACLUSZUVFCHUWSUW
      TUXQKUWAUWOUWCWDUVCVVLUWMUVCCUVNLEUWDUUGWJUWOUVFUVNCUVCUVOUWMUWNUUHEUUIUX
      OUWRCUVPDLUVFUWEFUUOWHYFUVJUXAUBUWRUHUVGUWRKUVIUWTUVEUVGUWRUVFUVHUUJUUKUU
      LTYRYSUUPUUMDURHUVMUVLSDUWHURFUWAUWHURHUWCUVPCUUQYTUURUADUVEUBUVEVAUUSYTU
      UT $.
      $( [2-Sep-2009] $)
  $}

  ${
    icccmp.1 $e |- J = ( A [,] B ) $.
    icccmp.2 $e |- M = ( ( abs o. - ) |` ( J X. J ) ) $.
    icccmp.3 $e |- T = ( MetOpen ` M ) $.
    $( A closed interval in ` RR ` is compact.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    icccmp $p |- ( ( A e. RR /\ B e. RR ) -> T e. Comp ) $=
      ( cr wcel wa cxp cres copn cfv ccomp wss syl ccld eqid co xpeq12i reseq2i
      cabs cmin ccom eqtri iccssre xpss12 syl11anc resabs1 eqcomd syl5eq fveq2d
      cicc wceq cbnd reheibor cioo crn ctg icccld tgioo fveq2i syl6eleq eqeltrd
      wb iccbnd mpbir2and ) AIJBIJKZCUDUEUFZIILZMZABUOUAZVNLZMZNOZPVJCENOVQHVJE
      VPNVJEVKVOMZVPEVKDDLZMVRGVSVOVKDVNDVNFFUBUCUGVJVOVLQZVRVPUPVJVNIQZWAVTABU
      HZWBVNIVNIUIUJZVTVPVRVKVOVLUKZULRUMUNUMVJVQPJZVNVMNOZSOZJZVPUQJZVJWAWEWHW
      IKVGWBVMVQWFVPVNVMTZVPTVQTWFTZURRVJVNUSUTVAOZSOWGABVBWLWFSVMWFWJWKVCVDVEV
      JVPVRUQVJVTVPVRUPWCWDRABVNVRVNTVRTVHVFVIVF $.
      $( [2-Sep-2009] $)
  $}

  $( The unit interval is compact.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iicmp $p |- II e. Comp $=
    ( cc0 cr wcel c1 cii ccomp 0re 1re cicc cabs cmin ccom cxp cres eqid icccmp
    co df-ii mp2an ) ABCDBCEFCGHADEADIQZJKLTTMNZTOUAORPS $.
    $( [2-Sep-2009] $)

