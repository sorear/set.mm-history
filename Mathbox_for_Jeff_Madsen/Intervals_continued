$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intervals (continued)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d R k u v x y $.  $d F k u v $.
    ismrer1.1 $e |- R = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    ismrer1.2 $e |- F = { <. x , y >. | ( x e. RR /\
                                              y = ( { 1 } X. { x } ) ) } $.
    $( An isometry between ` RR ` and ` RR ^ 1 ` .  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    ismrer1 $p |- F e. ( R Ismty ( Rn ` 1 ) ) $=
      ( vu vv vk c1 crrn cfv cismty co wcel cr csn cmap wf1o cv wceq wral cme
      wa wb remet cn 1nn rrnmet ax-mp remetba cdm cfz rrndm cz 1z fzsn opreq2i
      eqtr2i isismty mp2an wfn crn wi dff1o6 cxp snex xpex fnopab2 wrex cab
      cop cc ax-1cn elisseti visset xpsn eqeq2i rexbii abbii copab rneqi
      rnopab2 eqtri reex mapsn 3eqtr4i sneq xpeq2 syl fvopab4 eqeqan12d c0 wne
      snnz xp11 sneqr adantl sylbi syl6bi rgen2a mpbir3an cmin cabs c2 cexp
      csu csqr cvv oprex fveq2 opreq12d opreq1d fsum1i fveq2i a1i fveq1d snid
      fvconst2 syl6eq opreqan12d fveq2d resubcl absresq eqcomd cc0 cle wbr
      recn abscl absge0 sqrsq syl11anc eqtrd 3eqtrrd remetdval wf f1of
      ffvelrni syl6eleqr adantr rrnmval syl111anc 3eqtr4d mpbir2an ) DCJKLZMNOZ
      PPJQZRNZDSZGTZHTZCNZUUKDLZUULDLZUUFNZUAZHPUBGPUBZCUCOUUFUCOZUUGUUJUURUDUE
      CEUFJUGOZUUSUHJUIUJGHDCUUFPUUICEUKUUFULULZPJJUMNZRNZUUIUUTUVAUVCUAUHJUNUJ
      UVBUUHPRJUOOZUVBUUHUAUPJUQUJURZUSUTVAUUJDPVBDVCZUUIUAUUNUUOUAZUUKUULUAZVD
      ZHPUBGPUBGHPUUIDVEABPUUHATZQZVFZDUUHUVKJVGZUVJVGVHFVIBTZUVLUAZAPVJZBVKZUV
      NJUVJVLQZUAZAPVJZBVKUVFUUIUVPUVTBUVOUVSAPUVLUVRUVNJUVJJVMVNVOZAVPVQVRVSVT
      UVFUVJPOUVOUDABWAZVCUVQDUWBFWBABPUVLWCWDAPJBWEUWAWFWGUVIGHPUUKPOZUULPOZUD
      ZUVGUUHUUKQZVFZUUHUULQZVFZUAZUVHUWCUWDUUNUWGUUOUWIABUUKUVLUWGPDUVJUUKUAUV
      KUWFUAUVLUWGUAUVJUUKWHUVKUWFUUHWIWJFUUHUWFUVMUUKVGVHWKZABUULUVLUWIPDUVJUU
      LUAUVKUWHUAUVLUWIUAUVJUULWHUVKUWHUUHWIWJFUUHUWHUVMUULVGVHWKZWLUWJUUHUUHUA
      ZUWFUWHUAZUDZUVHUUHWMWNUWFWMWNUWJUWOUEJUWAWOUUKGVPZWOUUHUWFUUHUWHWPVAUWNU
      VHUWMUUKUULUWPWQWRWSWTXAXBZUUQGHPUWEUUKUULXCNZXDLZUVBITZUUNLZUWTUUOLZXCNZ
      XEXFNZIXGZXHLZUUMUUPUWEUXFJUUNLZJUUOLZXCNZXEXFNZXHLZUWRXEXFNZXHLZUWSUXFUX
      KUAUWEUXEUXJXHUXJXIOUVDUXEUXJUAUXIXEXFXJUPUXDUXJXIIJUWTJUAZUXCUXIXEXFUXNU
      XAUXGUXBUXHXCUWTJUUNXKUWTJUUOXKXLXMXNVAXOXPUWEUXJUXLXHUWEUXIUWRXEXFUWCUWD
      UXGUUKUXHUULXCUWCUXGJUWGLZUUKUWCJUUNUWGUWKXQJUUHOZUXOUUKUAJUWAXRZUUHUUKJU
      WPXSUJXTUWDUXHJUWILZUULUWDJUUOUWIUWLXQUXPUXRUULUAUXQUUHUULJHVPXSUJXTYAXMY
      BUWEUWRPOZUXMUWSUAUUKUULYCUXSUXMUWSXEXFNZXHLZUWSUXSUXLUXTXHUXSUXTUXLUWRYD
      YEYBUXSUWSPOZYFUWSYGYHZUYAUWSUAUXSUWRVMOZUYBUWRYIZUWRYJWJUXSUYDUYCUYEUWRY
      KWJUWSYLYMYNWJYOUUKUULCEYPUWEUUTUUNUVCOZUUOUVCOZUUPUXFUAUUTUWEUHXPUWCUYFU
      WDUWCUUNUUIUVCPUUIUUKDUUJPUUIDYQUWQPUUIDYRUJZYSUVEYTUUAUWDUYGUWCUWDUUOUUI
      UVCPUUIUULDUYHYSUVEYTWRIJUUNUUOUUBUUCUUDXAUUE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R x y f $.  $d T f $.  $d Y f $.
    reheibor.1 $e |- R = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    reheibor.2 $e |- M = ( R |` ( Y X. Y ) ) $.
    reheibor.3 $e |- T = ( MetOpen ` M ) $.
    reheibor.4 $e |- U = ( MetOpen ` R ) $.
    $( Heine-Borel theorem for real numbers.  A subset of ` RR ` is compact iff
       it is closed and bounded.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    reheibor $p |- ( Y C_ RR -> ( T e. Comp <->
                                  ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( vx vy vf cr wss c1 crrn cfv cv wcel csn cxp wceq wa copab cima cres
      copn ccomp ccld cbnd cn cfz co cmap wb 1nn crn imassrn wf eqid fconstg
      snssi fss syl11anc reex snex elmap sylibr cz 1z fzsn ax-mp opreq2i
      syl6eleqr fopab frn sstri a1i rrnheibor sylancr ctop chomeosm cme remet
      metres eqeltri opntop rrnmet pm3.2i cismty ismrer1 id remetba ismtyres
      syl22anc wi ismtyhmeo mp2an syl chomeo wbr wex cvv opabex2 resexg eleq1
      cla4ev hmph syl5ibr imp comptoppr 3expa syldan cuni uniopn2 eqcomi
      hmeocld ismtybnd syl111anc anbi12d 3bitr4d ) EMNZOPQZJRZMSZKROTZYDTZUAZUB
      UCJKUDZEUEZYJUAUFZUGQZUHSZYJYCUGQZUIQSZYKUJSZUCZBUHSZECUIQSZDUJSZUCYBOUKS
      ZYJMOOULUMZUNUMZNZYMYQUOUPUUDYBYJYIUQZUUCYIEURMUUCYIUSUUEUUCNJKMUUCYHYIYI
      UTZYEYHMYFUNUMZUUCYEYFMYHUSZYHUUGSYEYFYGYHUSYGMNUUHYFYDMVAYDMVBYFYGMYHVCV
      DMYFYHVEOVFVGVHUUBYFMUNOVISUUBYFUBVJOVKVLVMVNVOMUUCYIVPVLVQVRYLYNYKOUUCYJ
      UUCUTYKUTZYLUTZYNUTZVSVTYBBWASZYLWASZUCZYIEUFZBYLWBUMZSZYRYMUOZUULUUMDWCS
      ZUULDAEEUAUFZWCGAWCSZUUTWCSAFWDZAEWEVLWFZDBHWGVLYKWCSZUUMYCWCSZUVDUUAUVEU
      POWHVLZYCYJWEVLZYKYLUUJWGVLWIYBUUODYKWJUMSZUUQYBUVAUVEYIAYCWJUMSZYBUVHUVA
      YBUVBVRUVEYBUVFVRUVIYBJKAYIFUUFWKZVRYBWLZEYJDYKYIAYCMAFWMZYJUTGUUIWNWOZUU
      SUVDUVHUUQWPUVCUVGUUOBYLDYKHUUJWQWRWSUUNUUQBYLWTXAZUURUUNUUQUVNUUQUVNUUNL
      RZUUPSZLXBUVPUUQLUUOYIXCSUUOXCSJKMYHVEXDYIEXCXEVLUVOUUOUUPXFXGLBYLXHXIXJU
      ULUUMUVNUURBYLXKXLXMVTYBYSYOYTYPYBCWASZYNWASZYICYNWBUMSZYBYSYOUOUVQYBUVAU
      VQUVBACIWGVLVRUVRYBUVEUVRUVFYCYNUUKWGVLVRUVSYBUVIUVSUVJUVAUVEUVIUVSWPUVBU
      VFYICYNAYCIUUKWQWRVLVRUVKEYICYNMCXNZMUVAUVTMUBUVBACMUVLIXOVLXPXQWOYBUUSUV
      DUVHYTYPUOUUSYBUVCVRUVDYBUVGVRUVMUUODYKXRXSXTYA $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d J x y z $.  $d M x y z $.
    iccbnd.1 $e |- J = ( A [,] B ) $.
    iccbnd.2 $e |- M = ( ( abs o. - ) |` ( J X. J ) ) $.
    $( A closed interval in ` RR ` is bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccbnd $p |- ( ( A e. RR /\ B e. RR ) -> M e. Bnd ) $=
      ( vx vy vz cr wcel wa cdm cv cbl cfv co wceq crp wrex wral cbnd cicc
      cabs cmin ccom cxp cres cme cc wss eqid cnmet iccssre ax-resscn syl6ss
      cnmetba metssba2 sylancr xpeq12i reseq2i eqtri dmeqi syl6eqr eleq2d cle
      wbr w3a elicc2 c1 caddc cc0 clt resubcl ancoms recn abscl 3syl 1re
      readdcl sylancl a1i absge0 lt01 addgegt0 syl22anc elrp sylanbrc adantr
      cin wb 3ad2ant1 ad2antlr ad2antrr elbl sseld imp adantlr cnmetdval
      syl2an 3ad2antr1 3ad2antl1 adantll cneg absle syl11anc simplr2 simpr3
      simpr1 simpllr leneg mpbid wi simplll renegcl syl simplr1 le2add mp2and
      ad3antrrr adantl negcl addcom negsubdi eqtr4d negsub 3brtr3d simplr3
      simpr2 simpll simp1 mpbir2and leabs letrd ltp1 lelttrd eqbrtrd ex sylbid
      ssrdv sseqin2 sylib ineq2i syl6reqr 3eqtr4rd syl5eqss biimpar syl6eleqr
      blssp opreq2 eqeq2d rcla4ev sylbird r19.21aiv metres ax-mp eqeltri
      isbnd2 sylibr ) AJKZBJKZLZDMZMZGNZHNZDOPZQZRZHSTZGUVDUAZDUBKZUVBUVJGUVDUV
      BUVEUVDKUVEABUCQZKZUVJUVBUVMUVDUVEUVBUVMUDUEUFZUVMUVMUGZUHZMZMZUVDUVBUVOU
      IKZUVMUJUKUVMUVSRUVOUVOULZUMZUVBUVMJUJABUNUOUPZUVOUJUVMUVOUWAUQZURUSZUVCU
      VRDUVQDUVOCCUGZUHZUVQFUWFUVPUVOCUVMCUVMEEUTVAVBVCVCZVDVEUVBUVNUVEJKZAUVEV
      FVGZUVEBVFVGZVHZUVJABUVEVIZUVBUWLUVJUVBUWLLZBAUEQZUDPZVJVKQZSKZUVDUVEUWQU
      VGQZRZUVJUVBUWRUWLUVBUWQJKZVLUWQVMVGZUWRUVBUWPJKZVJJKZUXAUVBUWOJKZUWOUJKZ
      UXCUVAUUTUXEBAVNVOZUWOVPZUWOVQVRZVSUWPVJVTWAZUVBUXCUXDVLUWPVFVGZVLVJVMVGZ
      UXBUXIUXDUVBVSWBUVBUXEUXFUXKUXGUXHUWOWCVRUXLUVBWDWBUWPVJWEWFZUWQWGWHWIZUW
      NUVDUVEUWQUVOOPQZCWJZUWSUWNUXOUVMWJZUVMUXPUVDUWNUVMUXOUKUXQUVMRUWNIUVMUXO
      UWNINZUVMKZUXRUXOKZUWNUXSLZUXTUXRUJKZUVEUXRUVOQZUWQVMVGZUYAUVTUVEUJKZUXAU
      XBUXTUYBUYDLWKUVTUYAUWBWBUWLUYEUVBUXSUWIUWJUYEUWKUVEVPZWLZWMUVBUXAUWLUXSU
      XJWNUVBUXBUWLUXSUXMWNUXRUVOUVEUWQUJUWDWOWFUVBUXSUYBUWLUVBUXSUYBUVBUVMUJUX
      RUWCWPWQWRUWNUXSUYDUWNUXSUXRJKZAUXRVFVGZUXRBVFVGZVHZUYDUVBUXSUYKWKUWLABUX
      RVIWIUWNUYKUYDUWNUYKLZUYCUVEUXRUEQZUDPZUWQVMUWLUYKUYCUYNRZUVBUWIUWJUYKUYO
      UWKUWIUYIUYHUYOUYJUWIUYEUYBUYOUYHUYFUXRVPZUVEUXRUVOUWAWSWTXAXBXCUYLUYNUWP
      UWQUWLUYKUYNJKZUVBUWIUWJUYKUYQUWKUWIUYIUYHUYQUYJUWIUYHLUYMJKZUYMUJKUYQUVE
      UXRVNZUYMVPUYMVQVRXAXBXCZUVBUXCUWLUYKUXIWNZUVBUXAUWLUYKUXJWNUYLUYNUWOUWPU
      YTUVBUXEUWLUYKUXGWNZVUAUYLUYNUWOVFVGZUWOXDZUYMVFVGZUYMUWOVFVGZUYLUYRUXEVU
      CVUEVUFLWKUWLUYKUYRUVBUWIUWJUYKUYRUWKUWIUYIUYHUYRUYJUYSXAXBXCVUBUYMUWOXEX
      FUYLABXDZVKQZUVEUXRXDZVKQZVUDUYMVFUYLUWJVUGVUIVFVGZVUHVUJVFVGZUWIUWJUWKUV
      BUYKXGUYLUYJVUKUWNUYHUYIUYJXHUYLUYHUVAUYJVUKWKUWNUYHUYIUYJXIZUUTUVAUWLUYK
      XJZUXRBXKXFXLUYLUUTVUGJKZUWIVUIJKZUWJVUKLVULXMUUTUVAUWLUYKXNUYLUVAVUOVUNB
      XOXPUWIUWJUWKUVBUYKXQZUYLUYHVUPVUMUXRXOXPZAVUGUVEVUIXRWFXSUYLVUHVUGAVKQZV
      UDUYLAUJKZVUGUJKZVUHVUSRUUTVUTUVAUWLUYKAVPXTZUYLBUJKZVVAUVBVVCUWLUYKUVAVV
      CUUTBVPYAWNZBYBXPAVUGYCXFUYLVVCVUTVUDVUSRVVDVVBBAYDXFYEUYLUYEUYBVUJUYMRUW
      LUYEUVBUYKUYGWMUYKUYBUWNUYHUYIUYBUYJUYPWLYAUVEUXRYFXFZYGUYLVUJBAXDZVKQZUY
      MUWOVFUYLUWKVUIVVFVFVGZVUJVVGVFVGZUWIUWJUWKUVBUYKYHUYLUYIVVHUWNUYHUYIUYJY
      IUWNUUTUYHUYIVVHWKUYKUUTUVAUWLYJUYHUYIUYJYKAUXRXKWTXLUYLUWIVUPUVAVVFJKZUW
      KVVHLVVIXMVUQVURVUNUUTVVJUVAUWLUYKAXOXTUVEVUIBVVFXRWFXSVVEUYLVVCVUTVVGUWO
      RVVDVVBBAYFXFYGYLUYLUXEUWOUWPVFVGVUBUWOYMXPYNUYLUXCUWPUWQVMVGVUAUWPYOXPYP
      YQYRYSWQYLYRYTUVMUXOUUAUUBUXPUXQRUWNCUVMUXOEUUCWBUVBUVDUVMRUWLUVBUVMUVSUV
      DUWEUWHUUDWIUUEUWNUVTCUJUKZUVECKUWRUWSUXPRUVTUWNUWBWBUVBVVKUWLUVBCUVMUJEU
      WCUUFWIUWNUVEUVMCUVBUVNUWLUWMUUGEUUHUXNUWQCUVODUJUVEUWDFUUIWFYEUVIUWTHUWQ
      SUVFUWQRUVHUWSUVDUVFUWQUVEUVGUUJUUKUULXFYRYSUUMUUNDUIKUVLUVKWKDUWGUIFUVTU
      WGUIKUWBUVOCUUOUUPUUQGDUVDHUVDULUURUUPUUS $.
      $( [2-Sep-2009] $)
  $}

  ${
    icccmp.1 $e |- J = ( A [,] B ) $.
    icccmp.2 $e |- M = ( ( abs o. - ) |` ( J X. J ) ) $.
    icccmp.3 $e |- T = ( MetOpen ` M ) $.
    $( A closed interval in ` RR ` is compact.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    icccmp $p |- ( ( A e. RR /\ B e. RR ) -> T e. Comp ) $=
      ( cr wcel wa cabs cmin ccom cxp cres cicc co copn cfv ccomp xpeq12i
      reseq2i eqtri wss wceq iccssre xpss12 syl11anc resabs1 eqcomd syl syl5eq
      fveq2d ccld cbnd wb eqid reheibor cioo crn ctg icccld tgioo fveq2i
      syl6eleq iccbnd eqeltrd mpbir2and ) AIJBIJKZCLMNZIIOZPZABQRZVNOZPZSTZUAVJ
      CESTVQHVJEVPSVJEVKVOPZVPEVKDDOZPVRGVSVOVKDVNDVNFFUBUCUDVJVOVLUEZVRVPUFVJV
      NIUEZWAVTABUGZWBVNIVNIUHUIZVTVPVRVKVOVLUJZUKULUMUNUMVJVQUAJZVNVMSTZUOTZJZ
      VPUPJZVJWAWEWHWIKUQWBVMVQWFVPVNVMURZVPURVQURWFURZUSULVJVNUTVAVBTZUOTWGABV
      CWLWFUOVMWFWJWKVDVEVFVJVPVRUPVJVTVPVRUFWCWDULABVNVRVNURVRURVGVHVIVH $.
      $( [2-Sep-2009] $)
  $}

  $( The unit interval is compact.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iicmp $p |- II e. Comp $=
    ( cc0 cr wcel c1 cii ccomp 0re 1re cicc co cabs cmin ccom cxp cres eqid
    df-ii icccmp mp2an ) ABCDBCEFCGHADEADIJZKLMTTNOZTPUAPQRS $.
    $( [2-Sep-2009] $)

