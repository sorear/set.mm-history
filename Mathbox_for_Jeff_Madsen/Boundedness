$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Boundedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TotBnd $.
  $c Bnd $.
  $( Extend class notation with the class of totally bounded metric spaces. $)
  ctotbnd $a class TotBnd $.
  $( Extend class notation with the class of bounded metric spaces. $)
  cbnd $a class Bnd $.

  ${
    $d m d v b x $.
    $( Define the class of totally bounded metrics.  A metric space is totally
       bounded iff it can be covered by a finite number of balls of any given
       radius. $)
    df-totbnd $a |- TotBnd = { m e. Met | A. d e. RR+ E. v e. Fin ( U. v =
      dom dom m /\ A. b e. v E. x e. dom dom m b = ( x ( ball ` m ) d ) ) } $.
  $}

  ${
    $d M m d v b x $.  $d X m d v b x $.
    istotbnd.1 $e |- X = dom dom M $.
    $( The predicate "is a totally bounded metric space".  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    istotbnd $p |- ( M e. TotBnd <-> ( M e. Met /\ A. d e. RR+ E. v e. Fin
        ( U. v = X /\ A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( vm cv cdm wceq cbl cfv co wrex wral wa cfn crp eqeq2d cuni ctotbnd dmeq
      cme dmeqd syl6eqr fveq2 oveqd rexeqbidv ralbidv anbi12d rexbidv df-totbnd
      elrab2 ) BIZUAZHIZJZJZKZEIZAIZFIZUQLMZNZKZAUSOZEUOPZQZBROZFSPUPDKZVAVBVCC
      LMZNZKZADOZEUOPZQZBROZFSPHCUDUBUQCKZVJVRFSVSVIVQBRVSUTVKVHVPVSUSDUPVSUSCJ
      ZJDVSURVTUQCUCUEGUFZTVSVGVOEUOVSVFVNAUSDWAVSVEVMVAVSVDVLVBVCUQCLUGUHTUIUJ
      UKULUJABHEFUMUN $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M d v b x $.  $d X d v b x $.
    istotbnd2.1 $e |- X = dom dom M $.
    $( The predicate "is a totally bounded metric space."  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    istotbnd2 $p |- ( M e. Met -> ( M e. TotBnd <-> A. d e. RR+ E. v e. Fin
        ( U. v = X /\ A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( ctotbnd wcel cme cv cuni wceq cbl cfv co wrex wral wa cfn istotbnd baib
      crp ) CHICJIBKZLDMEKAKFKCNOPMADQEUDRSBTQFUCRABCDEFGUAUB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M d v b x $.
    $( The predicate "totally bounded" implies ` M ` is a metric space.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    totbndmet $p |- ( M e. TotBnd -> M e. Met ) $=
      ( vv vb vx vd ctotbnd wcel cme cv cuni cdm wceq cbl cfv wrex wral cfn crp
      co wa eqid istotbnd simplbi ) AFGAHGBIZJAKKZLCIDIEIAMNSLDUEOCUDPTBQOERPDB
      AUECEUEUAUBUC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X a b c d r s t u v w x y z $.  $d M a b c d r s t u v w x y z $.
    $d Y a b c d r s t u v w x y z $.  $d N a b c d r s t u v w x y z $.
    sstotbnd.1 $e |- X = dom dom M $.
    sstotbnd.2 $e |- N = ( M |` ( Y X. Y ) ) $.
    $( Condition for a subset of a metric space to be totally bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    sstotbnd $p |- ( ( M e. Met /\ Y C_ X ) -> ( N e. TotBnd <->
                        A. d e. RR+ E. v e. Fin ( Y C_ U. v /\
                          A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( vs vt vy wcel wss wa wceq wrex wral cfn vw vu vz va vc cme ctotbnd cuni
      vr cv cbl cfv co crp cdm wb cxp cres metres adantr syl5eqel istotbnd2 syl
      eqid w3a cvv simplr resexg dmexg 3syl ad2antrr simprr indexfi syl3anc cab
      wi abrexfi adantl ad2antlr metssba2 syl6reqr eqeq2d biimpa eqcomd adantlr
      dmeqi vex eqeq1 rexbidv elab biimpri ralimi dfss3 sylibr oveq1 mpan2 ovex
      rcla4ev cin simpr sseq2d ssel2 sylan adantllr anassrs a1i eqsstrd syl2anc
      blssp rexlimdva syl5bi ralrimiv adantrr adantlrr adantll ssrexv 3ad2antr1
      ex cbvrexv unieq raleq anbi12d mpd c2 c0 ineq1 neeq1d elrab sylan2 eluni2
      wne jcad wex elin syl6 imp rcla4cva ad2ant2rl eleq2 syld syldan imdistani
      ad2antll uniss an32s eqtrd inss1 sseq2 uniss2 sstrd 3adantr3 biimpd sstr2
      impcom syl6ib syl12anc ralimdva sylbid cdiv 2re 2pos elrpii rpdivcl oveq2
      ralbidv anbi2d rcla4v crab inelcm expcom reximdv2 3imtr4g ralimia 3imtr4i
      ssrab2 ssralv ax-mp ancom bitri exbii n0 df-rex 3bitr4i biimpi sylbi rgen
      3jca jctild 3anbi123d ssexg ancoms 3expb an12s blssm 3com23 3expia eleq2d
      ssfi a1d imbi12d mpbid sseq1 syl5ibrcom unissb sylib elequ1 elequ2 syl5bb
      3ad2antl2 adantlll id ad2ant2lr biimpcd reximdv blhalf an4s sseld expimpd
      ancomsd sylanr1 adantlrl simpll sylanbrc 3ad2antl1 ancom2s eleqtrrd ssrdv
      adantrl eqssd ad3antrrr eqtr4d biimpar jctird com23 exp3a 3impd ralrimdva
      eqeq1d expdimp sylibrd impbid ) CUFNZFEOZPZDUGNZFBUJZUHZOZGUJZAUJZHUJZCUK
      ULZUMZQZAERZGVUFSZPZBTRZHUNSZVUDVUEUAUJZUHZDUOZUOZQZUBUJZKUJZVUKDUKULZUMZ
      QZKVVCRUBVUTSZPZUATRZHUNSZVUSVUDDUFNZVUEVVMUPVUDDCFFUQZURZUFJVUBVVPUFNVUC
      CFUSUTVAZKUADVVCUBHVVCVDZVBVCVUDVVLVURHUNVUDVUKUNNZPZVVKVURUATVVTVUTTNZPZ
      VVKVURVWBVVKPZUCUJZVVCOZVVIKVWDRZUBVUTSZVVIUBVUTRKVWDSZVEZUCTRZVURVWCVWAV
      VCVFNZVVJVWJVVTVWAVVKVGVVTVWKVWAVVKVUBVWKVUCVVSVUBDVFNVVBVFNVWKVUBDVVPVFJ
      CVVOUFVHVADVFVIVVBVFVIVJVKVKVWBVVDVVJVLVVIUBKVUTVVCVFUCVMVNVWBVVDVWJVURVP
      VVJVWBVVDPVWIVURUCTVWBVVDVWDTNZVWIVURVPVWBVVDVWLPZPZVWIVURVWNVWIPUDUJZLUJ
      ZVUKVULUMZQZLVWDRZUDVOZTNZFVWTUHZOZVUOGVWTSZVURVWMVXAVWBVWIVWLVXAVVDLUDVW
      DVWQVQVRVSVWNVWEVWGVXCVWHVWBVVDVWEVWGPZVXCVWLVVTVVDVXEVXCVWAVVTVVDPZVXEPF
      VVAVXBVXFFVVAQZVXEVUDVVDVXGVVSVUDVVDPVVAFVUDVVDVVAFQVUDVVCFVVAVUDFVVPUOZU
      OVVCCEFIVTVVBVXHDVVPJWFWFWAZWBWCWDWEUTVVTVXEVVAVXBOVVDVVTVXEPZVVAVUIVVHQZ
      KVWDRZGVOZUHZVXBVXJVUTVXMOZVVAVXNOVWGVXOVVTVWEVWGVVEVXMNZUBVUTSVXOVWFVXPU
      BVUTVXPVWFVXLVWFGVVEUBWGVUIVVEQVXKVVIKVWDVUIVVEVVHWHWIWJWKWLUBVUTVXMWMWNU
      UCVUTVXMUUDVCVXJVUJMUJZOZMVWTRZAVXMSZVXNVXBOVVTVWEVXTVWGVVTVWEPZVXSAVXMVU
      JVXMNVUJVVHQZKVWDRZVYAVXSVXLVYCGVUJAWGZVUIVUJQVXKVYBKVWDVUIVUJVVHWHWIWJVY
      AVYBVXSKVWDVYAVVFVWDNZPZVYBVXSVYFVYBPZVVFVUKVULUMZVWTNZVUJVYHOZVXSVYEVYIV
      YAVYBVYEVYHVWQQZLVWDRZVYIVYEVYHVYHQZVYLVYHVDVYKVYMLVVFVWDVWPVVFQZVWQVYHVY
      HVWPVVFVUKVULWOWBWRWPVWSVYLUDVYHVVFVUKVULWQVWOVYHQVWRVYKLVWDVWOVYHVWQWHWI
      WJWNVSVYGVUJVYHFWSZVYHVYGVUJVVHVYOVYFVYBWTVYFVVHVYOQZVYBVYAVYEVVFFNZVYPVU
      DVWEVYEVYQVVSVUDVWEPZVWDFOZVYEVYQVUDVWEVYSVUDVVCFVWDVXIXAZWCVWDFVVFXBXCXD
      VVTVYQVYPVWEVUDVYQVVSVYPVUDVYQVVSVYPVUKFCDEVVFIJXIXEUUEWEUUAUTUUFVYOVYHOV
      YGVYHFUUGXFXGVXRVYJMVYHVWTVXQVYHVUJUUHWRXHXRXJXKXLXMAMVXMVWTUUIVCUUJWEXGX
      DXNUUKVWNVWGVWEVXDVWHVWBVWEVXDVWMVVTVWEVXDVWAVUDVWEVXDVVSVYRVUDVYSPVWDEOZ
      VXDVUDVWEVYSVUDVWEVYSVYTUULUUBVUCVYSWUAVUBVYSVUCWUAVWDFEUUMUUNXOWUAVUOGVW
      TVUIVWTNVUIVWQQZLVWDRZWUAVUOVWSWUCUDVUIGWGVWOVUIQVWRWUBLVWDVWOVUIVWQWHWIW
      JWUAWUCWUBLERVUOWUBLVWDEXPWUBVUNLAEVWPVUJQVWQVUMVUIVWPVUJVUKVULWOWBXSUUOX
      KXLVJWEWEWEXQVUQVXCVXDPBVWTTVUFVWTQZVUHVXCVUPVXDWUDVUGVXBFVUFVWTXTXAVUOGV
      UFVWTYAYBWRUUPXRXEXJXMYCXRXJUUQUURVUDVUSVVDVVFVUKUEUJZVVGUMZQZHVVCRZKVUTS
      ZPZUATRZUEUNSZVUEVUDVUSWUKUEUNVUDWUEUNNZPZVUSVUHVUIVUJWUEYDUUSUMZVULUMZQZ
      AERZGVUFSZPZBTRZWUKWUMVUSWVAVPZVUDWUMWUOUNNZWVBWUMYDUNNWVCYDUUTUVAUVBWUEY
      DUVCWPVURWVAHWUOUNVUKWUOQZVUQWUTBTWVDVUPWUSVUHWVDVUOWURGVUFWVDVUNWUQAEWVD
      VUMWUPVUIVUKWUOVUJVULUVDWBWIUVEUVFWIUVGVCVRWUNWUTWUKBTWUNVUFTNZPZWUTFVVEU
      HZOZWURGVVESZVXQVWPNZMFRZLVVESZVEZUBTRZWUKWVFWUTVWOFWSZYEYKZUDVUFUVHZTNZF
      WVQUHZOZWURGWVQSZWVKLWVQSZVEZPWVNWVFWUTWWCWVRWUTWWCVPWVFWUTWVTWWAWWBVUHWV
      TWUSVXQVUGNZMFSVXQWVSNZMFSVUHWVTWWDWWEMFVXQFNZVXQVUJNZAVUFRWWGAWVQRWWDWWE
      WWFWWGWWGAVUFWVQWWFVUJVUFNZWWGPZVUJWVQNZWWGWWIWWFWWJWWHWWGWWFWWJWWGWWFPWW
      HVUJFWSZYEYKZWWJVXQVUJFUVIWWJWWHWWLPWVPWWLUDVUJVUFVWOVUJQZWVOWWKYEVWOVUJF
      YFYGYHWKYIXEUVJWWIWWGVPWWFWWHWWGWTXFYLUVKAVXQVUFYJAVXQWVQYJUVLUVMMFVUGWMM
      FWVSWMUVNUTWUSWWAVUHWVQVUFOZWUSWWAVPWVPUDVUFUVOZWURGWVQVUFUVPUVQVRWWBWUTW
      VKLWVQVWPWVQNVWPVUFNZVWPFWSZYEYKZPWVKWVPWWRUDVWPVUFVWOVWPQWVOWWQYEVWOVWPF
      YFYGYHWWRWVKWWPWWRWVKVXQWWQNZMYMWWFWVJPZMYMWWRWVKWWSWWTMWWSWVJWWFPWWTVXQV
      WPFYNWVJWWFUVRUVSUVTMWWQUWAWVJMFUWBUWCUWDVRUWEUWFXFUWGXFWVEWVRWUNWVEWWNWV
      RWWOVUFWVQUWRWPVRUWHWVMWWCUBWVQTVVEWVQQZWVHWVTWVIWWAWVLWWBWXAWVGWVSFVVEWV
      QXTXAWURGVVEWVQYAWVKLVVEWVQYAUWIWRYOWUNWVNWUKVPWVEWUNWVMWUKUBTWUNVVETNZPZ
      WVHWVIWVLWUKWXCWVHWVIWVLWUKVPWXCWVLWVHWVIPZWUKWUNWXBWVLWXDWUKVPZWUNWXBWVL
      PZVUFFOZWVJMVUFRZLVVESZWVJLVVERMVUFSZVEZBTRZWXEVUDWXFWXLVPZWUMVUDFVFNZWXM
      VUCVUBWXNVUBVUCEVFNWXNVUBECUOZUOZVFIVUBWXOVFNWXPVFNCUFVIWXOVFVIVCVAFEVFUW
      JYIUWKWXNWXFWXLWXBWXNWVLWXLWXBWXNWVLWXLWVJLMVVEFVFBVMUWLUWMXRVCUTWUNWXKWX
      EBTVUDWUMWVEWXKWXEVPVUDWUMWVEPZPZWXKWXEWXRWXKPZWXDVWOVWDWUEVVGUMZQZUCVUFR
      ZUDVOZTNZWYCUHZVVCQZWUHKWYCSZPZPWUKWXSWXDWYDWYHWXQWXDWYDVPZVUDWXKWVEWYIWU
      MWVEWYDWXDUCUDVUFWXTVQUWSVRVSWXSWXDWYFWYGWXSWXDWYFWXSWXDPZWYEFVVCWYJWYEFW
      XSWYEFOZWXDWXRWXIWXGWYKWXJVUDWUMWXGWYKWVEWUNWXGPZVUJFOZAWYCSWYKWYLWYMAWYC
      VUJWYCNVUJWXTQZUCVUFRZWYLWYMWYBWYOUDVUJVYDWWMWYAWYNUCVUFVWOVUJWXTWHWIWJWY
      LWYNWYMUCVUFWYLVWDVUFNZPWYMWYNWXTFOZWUNWXGWYPWYQWXGWYPPWUNVWDFNZWYQVUFFVW
      DXBWUNWYRWYQWUNVWDVVCNZWXTVVCOZVPZWYRWYQVPZVUDVVNWUMXUAVVQVVNWUMWYSWYTVVN
      WYSWUMWYTDVWDWUEVVCVVRUWNUWOUWPXCVUDXUAXUBUPWUMVUDWYSWYRWYTWYQVUDVVCFVWDV
      XIUWQVUDVVCFWXTVXIXAUWTUTUXAYPYIXEVUJWXTFUXBUXCXJXKXLAWYCFUXDWNXNXQUTWYJU
      AFWYEWYJVUTFNZVUTWYENZWYJXUCPZVUTVUKNZHWYCRZXUDXUEVUTVVFNZKVVERZXUGWXDXUC
      XUIWXSWVHXUCXUIWVIWVHXUCPVUTWVGNXUIFWVGVUTXBKVUTVVEYJUXEWEXOXUEXUHXUGKVVE
      WYJXUCVVFVVENZXUHXUGVPWYJXUCXUJPZPZXUHXUGXULXUHPZUIUJZVVFNZUIVUFRZXUGXULX
      UPXUHWXKWXDXUKXUPWXRWXKXUJXUPWXDXUCWXIWXGXUJXUPWXJWXHXUPLVVFVVEWXHXUNVWPN
      ZUIVUFRVYNXUPWVJXUQMUIVUFMUILUXFXSVYNXUQXUOUIVUFLKUIUXGWIUXHYQUXIYRUXJUTX
      UMXUOXUGUIVUFXULXUHXUNVUFNZXUOXUGVPZWYJXUKXUHXURPZXUSWYJXUKXUTPZPZXUOXUGX
      VBXUOPZXUNWUEVVGUMZWYCNZVUTXVDNZXUGXVAXUOXVEWYJXUTXVEXUKXUOXURXVEXUHXURXV
      DWXTQZUCVUFRZXVEXURXVDXVDQZXVHXVDVDXVGXVIUCXUNVUFVWDXUNQWXTXVDXVDVWDXUNWU
      EVVGWOWBWRWPWYBXVHUDXVDXUNWUEVVGWQVWOXVDQWYAXVGUCVUFVWOXVDWXTWHWIWJWNVRVS
      XOXVCVUTXUNWUEVULUMZFWSZXVDXVCVUTXVJNZXUCVUTXVKNWYJXVAXUOXVLWXRWXDXVAXUOP
      ZXVLWXKWXRWVIXVMXVLWVHXVAWXRWVIPXUJXUHPZXUOXVLXUJXUHXVNXUCXURXVNUXKUXLWXR
      WVIXVNXUOPZXVLWVIXVOPZWXRVUTWUPNZXUNWUPNZPZAERZXVLXVPVVFWUPQZAERZXVTWVIXV
      NXWBXUOWVIXUJXWBXUHWURXWBGVVFVVEVUIVVFQWUQXWAAEVUIVVFWUPWHWIYQXMXMXVOXWBX
      VTVPZWVIXUHXUOXWCXUJXUHXUOPZXWAXVSAEXWAXWDXVSXWAXUHXVQXUOXVRVVFWUPVUTYSVV
      FWUPXUNYSYBUXMUXNXOVRYCVUDWUMXVTXVLWVEWUNXVTXVLWUNXVSXVLAEWUNVUJENZPXVRXV
      QXVLVUBWUMXWEXVRXVQPXVLVPVUCVUBWUMPZXWEPXVRXVQXVLXWFXWEXVRXVQXVLVPXWFXWEX
      VRPPWUPXVJVUTVUBXWEWUMXVRWUPXVJOWUECEVUJXUNIUXOUXPUXQXEUXRXDUXSXJYPXNYIXE
      UXTUYAXDXEXVAXUCWYJXUOXUCXUJXUTUYBVSVUTXVJFYNUYCXVBXVDXVKQZXUOWYJXUTXWGXU
      KWXSXURXWGWXDXUHWXRWXKXURXWGWXKXURPWXRXUNFNZXWGWXGWXIXURXWHWXJVUFFXUNXBUY
      DVUDWUMXWHXWGWVEVUDWUMXWHXWGVUDXWHWUMXWGWUEFCDEXUNIJXIUYEXEXNYIXEYRUYHUTU
      YFXUFXVFHXVDWYCVUKXVDVUTYSWRXHXRXEXEXJYCXRXEXJYCHVUTWYCYJWNXRUYGUYIVUDVVC
      FQWXQWXKWXDVXIUYJUYKXRWXRWXIWXGWYGWXJVUDWXGWYGWXQVUDWXGPZWUHKWYCVVFWYCNVV
      FWXTQZUCVUFRZXWIWUHWYBXWKUDVVFKWGVWOVVFQWYAXWJUCVUFVWOVVFWXTWHWIWJXWKWUGH
      VUFRZXWIWUHXWJWUGUCHVUFVWDVUKQWXTWUFVVFVWDVUKWUEVVGWOWBXSXWIVUFVVCOZXWLWU
      HVPVUDXWMWXGVUDVVCFVUFVXIXAUYLWUGHVUFVVCXPVCXKXKXLWEXQUYMYLWUJWYHUAWYCTVU
      TWYCQZVVDWYFWUIWYGXWNVVAWYEVVCVUTWYCXTUYRWUHKVUTWYCYAYBWRYOXRXEXJYTUYSUYN
      UYOUYPXJUTYTXJYTUYQVUDVVNVUEWULUPVVQHUADVVCKUEVVRVBVCUYTVUA $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M n p q r s t u v w x y z $.  $d X n p q r s t u v w x y z $.
    $d S n p q r s t u v w x y z $.
    totbndss.1 $e |- X = dom dom M $.
    $( A subset of a totally bounded metric space is totally bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    totbndss $p |- ( ( M e. Met /\ M e. TotBnd /\ S C_ X ) ->
                                      ( M |` ( S X. S ) ) e. TotBnd ) $=
      ( vx vw vy vu vt wcel wss wi wa cv wceq wrex wral cfn cvv ex vv vz vq cme
      vr vp vn vs ctotbnd cxp cres cuni cbl cfv co crp cdm cdiv 2re 2pos elrpii
      c2 rpdivcl mpan2 oveq2 eqeq2d rexbidv ralbidv anbi2d rcla4v syl adantl c0
      cpw cin wne crab w3a ssrab2 ad2antlr dmexg syl5eqel ssexg mpan9 ad3antrrr
      ssfi pwexg ineq1 neeq1d elrab n0 eqeq1 rcla4cva adantll inss1 sseli eleq2
      wex syl5ibcom blhalf inss2 ovex inex1 elpw mpbir a1i sseq1 biimparc ssrin
      eqidd oveq1 ineq1d rcla4ev syl2anc 3ad2ant3 adantr sseq2 anbi12d ad2antrr
      3ad2antl2 an4s com23 imp anassrs rexlimdva exlimdv sylan2 sylan2b adantlr
      mpdd biimprd ancoms syl6ibr syld jcad sylibd eqid reximdva wb istotbnd2
      syl12anc 3exp1 exp3a an32s ralrimiva indexfi syl3anc uniss unipw syl6sseq
      mpd anasss 3ad2ant1 ssel eluni syl6ib ad2antrl inelcm an13s sseq1d anbi1d
      biimpri rcla4cv syl5 expdimp elin sylan2br expimpd reximdv eluni2 adantrr
      ssel2 ssrdv adantllr 3adantr3 eqss metssba2 blssp ancom2s rexeqdv adantld
      a1d ralimdva impr 3adantr2 ralrimdva metres 3imtr4d 3imp ) BUDJZBUIJZACKZ
      BAAUJUKZUIJZUWJUWLUWKUWNUWJUWLUWKUWNLUWJUWLMZUANZULZCOZUBNZENZUCNZBUMUNZU
      OZOZECPZUBUWPQZMZUARPZUCUPQZFNZULZUWMUQUQZOZGNZHNZUENZUWMUMUNUOZOZHUXLPZG
      UXJQZMZFRPZUEUPQZUWKUWNUWOUXIUYBUEUPUWOUXPUPJZMZUXIUWRUWSUWTUXPVBURUOZUXB
      UOZOZECPZUBUWPQZMZUARPZUYBUYDUXIUYLLZUWOUYDUYFUPJZUYMUYDVBUPJUYNVBUSUTVAU
      XPVBVCVDUXHUYLUCUYFUPUXAUYFOZUXGUYKUARUYOUXFUYJUWRUYOUXEUYIUBUWPUYOUXDUYH
      ECUYOUXCUYGUWSUXAUYFUWTUXBVEVFVGVHVIVGVJVKVLUYEUYKUYBUARUYEUWPRJZMZUYKUXJ
      AVNZKZINZAVOZUXNKZUXNUXOUXPUXBUOZAVOZOZHAPZMZGUXJPZIUFNZAVOZVMVPZUFUWPVQZ
      QZVUGIVULPZGUXJQZVRZFRPZUYBUYQUYKVUQUYQUYKMZVULRJZUYRSJZVUGGUYRPZIVULQZVU
      QUYPVUSUYEUYKUYPVULUWPKVUSVUKUFUWPVSUWPVULWFVDVTUWOVUTUYDUYPUYKUWOASJZVUT
      UWJCSJZUWLVVCUWJCBUQZUQZSDUWJVVESJVVFSJBUDWAVVESWAVKWBUWLVVDVVCACSWCTWDAS
      WGVKWEUYEUYKVVBUYPUYEUYKMZVVAIVULUYTVULJZVVGUYTUWPJZVUAVMVPZMVVAVUKVVJUFU
      YTUWPVUIUYTOVUJVUAVMVUIUYTAWHWIWJVVGVVIVVJVVAVVJVVGVVIMZUXJVUAJZFWRZVVAFV
      UAWKVVKVVMVVAUYEUYKVVIVVMVVALZUYKVVIMUYEUYTUYGOZECPZVVNUYJVVIVVPUWRUYIVVP
      UBUYTUWPUWSUYTOUYHVVOECUWSUYTUYGWLVGWMWNUYEVVPMZVVLVVAFVVQVVLVVAUYEVVLVVP
      VVAUYEVVLMZVVPVVAVVRVVOVVAECVVRUWTCJZMZVVOUXJUYGJZVVAVVLVVOVWALUYEVVSVVLU
      XJUYTJVVOVWAVUAUYTUXJUYTAWOWPUYTUYGUXJWQWSVTVVTVWAVVOVVAVVRVVSVWAVVOVVALZ
      LZUWOUYDVVLVVSVWCLZUWJUYDUWLVVLVWDUWJUYDMZUWLVVLMZMVVSVWAVWBVWEVWFVVSVWAM
      ZVWBLVWEVWGVWFVWBVWEVWGVWFVWBLZUWJVVSUYDVWAVWHUWJVVSMUYDVWAMZMUYGUXJUXPUX
      BUOZKZVWHUXPBCUWTUXJDWTUWJVWKVWHLVVSVWIUWJVWKVWFVVOVVAUWJVWKVWFVRZVVOMZVW
      JAVOZUYRJZVUAVWNKZVWNVUDOZHAPZVVAVWOVWMVWOVWNAKVWJAXAVWNAVWJAUXJUXPUXBXBX
      CXDXEXFVWKUWJVVOVWPVWFVWKVVOMUYTVWJKZVWPVVOVWSVWKUYTUYGVWJXGXHUYTVWJAXIVK
      XTVWLVWRVVOVWFUWJVWRVWKVVLVWRUWLVVLUXJAJVWNVWNOZVWRVUAAUXJUYTAXAWPVVLVWNX
      JVWQVWTHUXJAUXOUXJOZVUDVWNVWNVXAVUCVWJAUXOUXJUXPUXBXKXLVFXMXNVLXOXPVUGVWP
      VWRMGVWNUYRUXNVWNOZVUBVWPVUFVWRUXNVWNVUAXQVXBVUEVWQHAUXNVWNVUDWLVGXRXMUUA
      UUBXSUUKYATYBYCUUCYAYDYCYBYJYEYCUUDTYFYGYDYCYHUULYHUUEYIVUGIGVULUYRSFUUFU
      UGTUYQUYKVUQUYBLVURVUPUYAFRVURUXJRJZMZVUPUXMUXTVXDVUPUXKAOZUXMVXDVUPUXKAK
      ZAUXKKZMVXEVXDVUPVXFVXGVUPVXFLVXDUYSVUMVXFVUOUYSUXKUYRULAUXJUYRUUHAUUIUUJ
      UUMXFVXDVUPVXGVXDUYSVUMVXGVUOVURUYSVUMMZVXGVXCUYEUYKVXHVXGUYPVVGVXHMZUGAU
      XKVXIUGNZAJZVXJCJZVXJUXKJZUYEVXKVXLLZUYKVXHUWLVXNUWJUYDACVXJUUNVTXSVXIVXK
      VXLVXMLVXIVXKMZVXLVXJUHNZJZVXPUWPJZMZUHWRZVXMVVGVXLVXTLZVXHVXKUWRVYAUYEUY
      JUWRVXLVXJUWQJZVXTUWRVYBVXLUWQCVXJWQYKUHVXJUWPUUOUUPUUQXSVXOVXSVXMUHVXOVX
      SVXPAVOZUXNKZVUFMZGUXJPZVXMVXHVXKVXSVYFLZVVGVUMVXKVYGUYSVUMVXKVXSVYFVXKVX
      SMVXPVULJZVUMVYFVXRVXQVXKVYHVXQVXKMZVXRVYCVMVPZVYHVXJVXPAUURVYHVXRVYJMVUK
      VYJUFVXPUWPVUIVXPOVUJVYCVMVUIVXPAWHWIWJUVBYGUUSVUHVYFIVXPVULUYTVXPOZVUGVY
      EGUXJVYKVUBVYDVUFVYKVUAVYCUXNUYTVXPAWHUUTUVAVGUVCUVDUVEWNWNVXKVXSVYFVXMLZ
      LVXIVXKVXSVYLVXKVXQVYLVXRVXQVXKVYLVYIVYFVXJUXNJZGUXJPVXMVYIVYEVYMGUXJVYIV
      YDVUFVYMVYIVYDMVYMVUFVYDVYIVYMVYIVYDVXJVYCJVYMVXJVXPAUVFVYCUXNVXJUVLUVGYL
      UWBUVHUVIGVXJUXJUVJYMYLUVKTVLYJYFYNTYJUVMUVNYIUVOTYOUXKAUVPYMVXDAUXLUXKUY
      QAUXLOZUYKVXCUWOVYNUYDUYPBCADUVQZXSXSVFYPUYEVUPUXTLUYPUYKVXCUYEVUPUXTUYEU
      YSVUOUXTVUMUYEUYSVUOUXTUYEUYSMZVUNUXSGUXJVYPUXNUXJJZMZVUGUXSIVULVYRVUGUXS
      LVVHVYRVUFUXSVUBVYRVUFUXRHAPUXSVYRVUEUXRHAVYRUXOAJZMZUXRVUEVYTUXQVUDUXNVY
      PVYSUXQVUDOZVYQUYEVYSWUAUYSUWOUYDVYSWUAUWOVYSUYDWUAUXPABUWMCUXODUWMYQUVRU
      VSYDYIYIVFYKYRVYRUXRHAUXLUWOVYNUYDUYSVYQVYOWEUVTYPUWAXPYEUWCUWDUWETWEYOYR
      TYJYEYNUWFUWJUWKUXIYSUWLEUABCUBUCDYTXPUWJUWNUYCYSZUWLUWJUWMUDJWUBBAUWGHFU
      WMUXLGUEUXLYQYTVKXPUWHTYBUWI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d m x r $.
    $( Define the class of bounded metrics.  A metric space is bounded iff it
       can be covered by a single ball. $)
    df-bnd $a |- Bnd = { m e. Met | A. x e. dom dom m E. r e. RR+
                                          dom dom m = ( x ( ball ` m ) r ) } $.
  $}

  ${
    $d M m x r $.  $d X m x r $.
    isbnd.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isbnd $p |- ( M e. Bnd <-> ( M e. Met /\ A. x e. X E. r e. RR+
                            X = ( x ( ball ` M ) r ) ) ) $=
      ( vm cv cdm cbl cfv co wceq crp wrex wral cme cbnd dmeq dmeqd syl6eqr
      fveq2 oveqd eqeq12d rexbidv raleqbidv df-bnd elrab2 ) FGZHZHZAGZDGZUHIJZK
      ZLZDMNZAUJOCUKULBIJZKZLZDMNZACOFBPQUHBLZUPUTAUJCVAUJBHZHCVAUIVBUHBRSETZVA
      UOUSDMVAUJCUNURVCVAUMUQUKULUHBIUAUBUCUDUEAFDUFUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M m x r $.  $d X m x r $.
    isbnd2.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isbnd2 $p |- ( M e. Met -> ( M e. Bnd <->
                        A. x e. X E. r e. RR+ X = ( x ( ball ` M ) r ) ) ) $=
      ( cbnd wcel cme cv cbl cfv co wceq crp wrex wral isbnd baib ) BFGBHGCAIDI
      BJKLMDNOACPABCDEQR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x y r s $.  $d X x y r s $.
    isbnd3.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  Uses a single point instead
       of an arbitrary point in the space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isbnd3 $p |- ( ( M e. Bnd /\ X =/= (/) ) <-> ( M e. Met /\
                E. x e. X E. r e. RR+ X = ( x ( ball ` M ) r ) ) ) $=
      ( vy vs wcel wa cv co wceq crp wrex eqeq2d wi c2 wss a1i sylan2 cbnd wral
      c0 wne cme cbl isbnd anbi1i anass r19.2z ancoms oveq1 oveq2 cbvrex2v cmul
      cfv 2re 2pos elrpii rpmulcl mpan ad2antll ad2antrr cdiv cc0 rpcn 2cn 2ne0
      cc w3a divcan3 eqcomd syl3anc oveq2d biimpd adantr imp simpr eleq2 blhalf
      biimpac expr anasss anassrs eqsstrd syldan adantl blssm 3expa an32s eqssd
      rcla4ev syl2anc ex ralrimdva rexlimdvv syl5bi jcad impbid2 pm5.32i 3bitri
      rexn0 ) BUAHZCUCUDZIBUEHZCAJZDJZBUFUPZKZLZDMNZACUBZIZXDIXEXLXDIZIXEXKACNZ
      IXCXMXDABCDEUGUHXEXLXDUIXEXNXOXEXNXOXDXLXOXKACUJUKXEXOXLXDXOCFJZGJZXHKZLZ
      GMNFCNXEXLXJXSCXPXGXHKZLADFGCMXFXPLXIXTCXFXPXGXHULOXGXQLXTXRCXGXQXPXHUMOU
      NXEXSXLFGCMXEXPCHZXQMHZIZXSXLPXEYCIZXSXKACYDXFCHZIZXSXKYFXSIZQXQUOKZMHZCX
      FYHXHKZLZXKYDYIYEXSYBYIXEYAQMHYBYIQUQURUSQXQUTVAZVBVCYGCYJYFXSCXPYHQVDKZX
      HKZLZCYJRYFXSYOYDXSYOPZYEYBYPXEYAYBXSYOYBXRYNCYBXQYMXPXHYBXQVIHZQVIHZQVEU
      DZXQYMLXQVFYRYBVGSYSYBVHSYQYRYSVJYMXQXQQVKVLVMVNOVOVBVPVQYFYOICYNYJYFYOVR
      YDYEYOYNYJRZYEYOIYDXFYNHZYTYOYEUUACYNXFVSWAYDUUAYTXEYAYBUUAYTPZYBXEYAIZYI
      UUBYLUUCYIUUAYTYHBCXPXFEVTWBTWCVQTWDWEWFYFYJCRZXSXEYEYCUUDYCXEYEIYIUUDYBY
      IYAYLWGXEYEYIUUDBXFYHCEWHWITWJVPWKXJYKDYHMXGYHLXIYJCXGYHXFXHUMOWLWMWNWOWN
      WPWQXOXDPXEXKACXBSWRWSWTXA $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x y r $.  $d X x y r $.  $d S x y r $.
    bndss.1 $e |- X = dom dom M $.
    $( A subset of a bounded metric space is bounded.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    bndss $p |- ( ( M e. Bnd /\ S C_ X ) -> ( M |` ( S X. S ) ) e. Bnd ) $=
      ( vy vr vx cme wcel cv cbl cfv co wceq crp wrex wral wa cin an32s wss cxp
      cres cdm metres ad2antrr metssba2 eleq2d biimpar adantllr wi ssel2 ancoms
      cbnd oveq1 eqeq2d rexbidv rcla4va sylan adantlll eqid an4s anassrs adantr
      blssp dfss biimpi incom syl6eq ineq1 sylan9eq adantll adantlr 3eqtr2rd ex
      reximdva imp syldan ralrimiva jca isbnd anbi1i 3imtr4i ) BHIZCEJZFJZBKLZM
      ZNZFOPZECQZRZACUAZRZBAAUBUCZHIZWOUDUDZGJZWFWOKLMZNZFOPZGWQQZRBUNIZWMRWOUN
      IWNWPXBWDWPWKWMBAUEUFWNXAGWQWNWRWQIZWRAIZXAWDWMXDXEWKWDWMRZXEXDXFAWQWRBCA
      DUGZUHUIUJWLXEWMXAWLXERWMXAWDXEWKWMXAUKWDXERZWKRWMXAXHWMWKXAXHWMRZWKCWRWF
      WGMZNZFOPZXAXEWMWKXLWDXEWMRWRCIZWKXLWMXEXMACWRULUMWJXLEWRCWEWRNZWIXKFOXNW
      HXJCWEWRWFWGUOUPUQURUSUTXIXLXAXIXKWTFOXIWFOIZRZXKWTXPXKRWSXJASZAWQXPWSXQN
      ZXKXHWMXOXRWDWMXEXOXRWFABWOCWRDWOVAVEVBVCVDXIXKAXQNZXOWMXKXSXHWMXKACASZXQ
      WMAACSZXTWMAYANACVFVGACVHVICXJAVJVKVLVMXIAWQNZXOXKWDWMYBXEXGVMUFVNVOVPVQV
      RTVOTVQTVRVSVTXCWLWMEBCFDWAWBGWOWQFWQVAWAWC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x r $.  $d Y x r $.  $d R x r $.
    blbnd.1 $e |- X = dom dom M $.
    $( A ball is bounded.  (Contributed by Jeff Madsen, 2-Sep-2009.)  (The
       proof was shortened by Mario Carneiro, 15-Jan-2014.) $)
    blbnd $p |- ( ( M e. Met /\ Y e. X /\ R e. RR+ ) ->
          ( M |` ( ( Y ( ball ` M ) R ) X. ( Y ( ball ` M ) R ) ) ) e. Bnd ) $=
      ( vx vr cme wcel crp cbl cfv co cdm cv wceq wrex cin sylanbrc eqid w3a c0
      cxp cres cbnd wne metres 3ad2ant1 blcntr wss simp1 blssm metssba2 syl2anc
      wa eleqtrd simp3 simp2 elin blres syl3anc inidm syl6eq eqtr2d oveq1 oveq2
      eqeq2d rcla42ev isbnd3 simpld ) BHIZDCIZAJIZUAZBDABKLMZVOUCUDZUEIZVPNNZUB
      UFZVNVPHIZVRFOZGOZVPKLZMZPZGJQFVRQZVQVSUOVKVLVTVMBVOUGUHVNDVRIVMVRDAWCMZP
      ZWFVNDVOVRBDACEUIZVNVKVOCUJVOVRPVKVLVMUKZBDACEULBCVOEUMUNZUPVKVLVMUQZVNWG
      VOVRVNWGVOVORZVOVNVKDCVORIZVMWGWMPWJVNVLDVOIWNVKVLVMURWIDCVOUSSWLVPBDACVO
      EVPTUTVAVOVBVCWKVDWEWHVRDWBWCMZPFGDAVRJWADPWDWOVRWADWBWCVEVGWBAPWOWGVRWBA
      DWCVFVGVHVAFVPVRGVRTVISVJ $.
      $( [15-Jan-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d M a b c d m r s t u v x y z $.  $d X a b c d m r s t u v x y z $.
    totbndbnd.1 $e |- X = dom dom M $.
    $( A totally bounded metric space is bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    totbndbnd $p |- ( M e. TotBnd -> M e. Bnd ) $=
      ( vb vx va vc wcel wceq wrex wa crp c1 wi clt wbr c0 cr cc0 adantlr vv vd
      vy vr vu vs vz vt cme ctotbnd cbnd totbndmet cv cuni cbl cfv wral cfn 1rp
      oveq2 eqeq2d rexbidv ralbidv anbi2d rcla4v ax-mp wss w3a cvv simplr dmexg
      co cdm syl syl5eqel ad3antrrr simprr indexfi syl3anc wne crab cab abrexfi
      caddc cin dfrab3 inss2 eqsstri ssfi sylancl copab crn ssel2 metcl readdcl
      1re 0cn cle metge0 lt01 jctir 0re a1i leltadd syl22anc mpd syl5eqbrr elrp
      sylanbrc 3expa sylan2 anassrs rexlimdva 3ad2antr1 wn eqid eqeq1 sylan wor
      syl2anc eleq2 ad2antlr ad2antrr ax-17 hban elrab rpre ex adantl anasss wb
      imp adantllr adantr an32s syld com23 elbl 3imtr4d syl5 addid1i syl5ibrcom
      eleq1 pm4.71rd abbidv rnopab2 df-rab 3eqtr4g dmopab2 eqeq1i dm0rn0 bitr3i
      ovex rex0 rexeq mtbiri ne0i unieq uni0 syl6eq syl5ib necon3d mpan9 r19.2z
      eqidd reximi rexlimivw sylbir necon2ai adantlll 3ad2antr2 eqnetrrd ssrab2
      nsyl rpssre ltso soss fimaxg mp3an1 ssrexv eluni2 biimpi syl6bir rcla4cva
      sstri mp2 3ad2antl2 adantll hbre1 hbral hbra1 hb3an biimpa simplbi equcom
      eqle syl5bi idd necon3bd ltle syl2an ancoms imim12d pm2.61 sylsyld anim1i
      ralimdva peano2re oveq1d rcla4ev mpan2 breq1 rcla4va anim2i ltadd2 biimpd
      ancom2s 3ad2antl1 ltletr mettri 3exp2 imp41 lelttr mpand imdistand mp3an3
      exp3a exp4b rexlimd blssm sseld impbid eqrdv reximdva ralrimdva istotbnd2
      adantlrr mpdan isbnd2 mpcom ) AUIHZAUJHZAUKHZAULVUAUAUMZUNZBIZDUMZEUMZUBU
      MZAUOUPZVLZIZEBJZDVUDUQZKZUAURJZUBLUQZBUCUMZUDUMZVUJVLZIZUDLJZUCBUQVUBVUC
      VUAVUQVVBUCBVUQVUFVUGVUHMVUJVLZIZEBJZDVUDUQZKZUAURJZVUAVURBHZKZVVBMLHZVUQ
      VVHNUSVUPVVHUBMLVUIMIZVUOVVGUAURVVLVUNVVFVUFVVLVUMVVEDVUDVVLVULVVDEBVVLVU
      KVVCVUGVUIMVUHVUJUTVAVBVCVDVBVEVFVVJVVGVVBUAURVVJVUDURHZKZVVGVVBVVNVVGKZU
      EUMZBVGZVVDEVVPJZDVUDUQZVVDDVUDJZEVVPUQZVHZUEURJZVVBVVOVVMBVIHZVVFVWCVVJV
      VMVVGVJVUAVWDVVIVVMVVGVUABAVMZVMZVICVUAVWEVIHVWFVIHAUIVKVWEVIVKVNVOVPVVNV
      UFVVFVQVVDDEVUDBVIUEVRVSVVNVUFVWCVVBVVFVVJVUFVWCVVBVVMVVJVUFKZVWCVVBVWGVW
      BVVBUEURVWGVVPURHZKZVWBVVBVWIVWBKZVUSUFUMZVTZVWKVUSOPZNZUFFUMZVURGUMZAVLZ
      MWDVLZIZGVVPJZFLWAZUQZUDVXAJZVVBVWJVXAURHZVXAQVTZVXCVWJVWHVXDVWGVWHVWBVJV
      WHVWTFWBZURHVXAVXFVGVXDGFVVPVWRWCVXALVXFWEVXFVWTFLWFLVXFWGWHVXFVXAWIWJVNV
      WJVWPVVPHZVWSKGFWKZWLZVXAQVWJVXFVWOLHZVWTKZFWBZVXIVXAVWIVVSVVQVXFVXLIZVWA
      VWGVVQVXMVWHVVJVVQVXMVUFVVJVVQKZVWTVXKFVXNVWTVXJVXNVWSVXJGVVPVVJVVQVXGVWS
      VXJNZVVQVXGKVVJVWPBHZVXOVVPBVWPWMVVJVXPKVXJVWSVWRLHZVUAVVIVXPVXQVUAVVIVXP
      VHZVWRRHZSVWROPVXQVXRVWQRHZMRHZVXSVURVWPABCWNZWPVWQMWOWJVXRSSSWDVLZVWROSW
      QUUAZVXRSVWQWRPZSMOPZKZVYCVWROPZVXRVYEVYFVURVWPABCWSWTXAVXRSRHZVYIVXTVYAV
      YGVYHNVYIVXRXBXCZVYJVYBVYAVXRWPXCSSVWQMXDXEXFXGVWRXHXIXJVWOVWRLUUCUUBXKXL
      XMUUDUUETTXNGFVVPVWRUUFVWTFLUUGUUHVWIVVQVVSVXIQVTZVWAVWGVVSVYKVWHVVIVUFVV
      SVYKVUAVVIVUFKZVVSKZVXIQVXIQIZVVPQIZVYMXOVYOVXHVMZQIVYNVYPVVPQGFVVPVWRVXH
      VWQMWDUUMVXHXPUUIUUJVXHUUKUULVYOVUHVUHIZEVVPJZVYMVYOVYRVYQEQJVYQEUUNVYQEV
      VPQUUOUUPVYLVUDQVTZVVSVYRVVIBQVTVUFVYSBVURUUQVUFVUDQBQVUDQIZVUEQIVUFBQIVY
      TVUEQUNQVUDQUURUUSUUTVUEBQXQUVAUVBUVCVYSVVSKVVRDVUDJVYRVVRDVUDUVDVVRVYRDV
      UDVVDVYQEVVPVVDVUHUVEUVFUVGVNXRUVNUVHUVIUVJTUVKUVLVXAOXSZVXDVXEVXCVXARVGR
      OXSWUAVXALRVWTFLUVMZUVOUWEUVPVXAROUVQUWFUDUFVXAOUVRUVSXTVXCVXBUDLJZVWJVVB
      VXALVGVXCWUCNWUBVXBUDVXALUVTVFVWJVXBVVAUDLVWGVWBVUSLHZVXBVVANVWHVWGVWBKZW
      UDKZVXBVVAWUFVXBKZUGBVUTWUGUGUMZBHZWUHVUTHZWUGWUIWUHUHUMZHZUHVUDJZWUJWUEW
      UIWUMNZWUDVXBVUFWUNVVJVWBVUFWUIWUHVUEHZWUMVUEBWUHYAWUOWUMUHWUHVUDUWAUWBUW
      CYBYCWUGWULWUJUHVUDWUGWUKVUDHZKZWUKVVCIZEVVPJZWULWUJNZWUFWUPWUSVXBWUEWUPW
      USWUDVWBWUPWUSVWGVVSVVQWUPWUSVWAVVRWUSDWUKVUDVUGWUKIVVDWUREVVPVUGWUKVVCXQ
      VBUWDUWGUWHTTWUQWURWUTEVVPWUGWUPEWUFVXBEWUEWUDEVWGVWBEVWGEYDVVQVVSVWAEVVQ
      EYDVVREDVUDVUGVUDHEYDVVDEVVPUWIUWJVVTEVVPUWKUWLYEWUDEYDYEVXBEYDYEWUPEYDYE
      WUTEYDWUQVUHVVPHZWURWULWUJWURWULKWUHVVCHZWUQWVAKZWUJWURWULWVBWUKVVCWUHYAU
      WMWVCWUIVUHWUHAVLZMOPZKZWUIVURWUHAVLZVUSOPZKZWVBWUJWVCWUIWVEWVHWUGWVAWUIW
      VEWVHNZNZWUPWUGWVAKVURVUHAVLZMWDVLZVUSWRPZWVKWUFVXBWVAWVNWUEWUDVXBWVAKZWV
      NWUEWUDWVOKZWVNVWGVVSVVQWVPWVNNZVWAVVJVVQWVQVUFVXNWVPWVNWVPVXNVWKVUSWRPZU
      FVXAUQZWVAKZWVNWUDVXBWVAWVTWUDVXBKWVSWVAWUDVXBWVSWUDVWNWVRUFVXAVWKVXAHZWU
      DVWKLHZVWNWVRNWWAWWBVWKVWRIZGVVPJZVWTWWDFVWKLVWOVWKIVWSWWCGVVPVWOVWKVWRXQ
      VBYFUWNWUDWWBKZVUSVWKIZWVRNZVWNWWFXOZWVRNWVRWWBWWGWUDWWBVWKRHZWWGVWKYGZWW
      FVWKVUSIZWWIWVRUDUFUWOWWIWWKWVRVWKVUSUWPYHUWQVNYIWWEWWHVWLVWMWVRWWEWWFVUS
      VWKWWEWWFUWRUWSWWBWUDVWMWVRNZWWBWWIVUSRHZWWLWUDWWJVUSYGZVWKVUSUWTUXAUXBUX
      CWWFWVRUXDUXEXKUXGYLUXFYJVXNWVAWVSWVNVXNWVAWVSWVNVXNWVAKZWVMVXAHZWVSWVNWW
      OWVMLHZWVMVWRIZGVVPJZWWPVVJVVQWVAWWQVVQWVAKVVJVUHBHZWWQVVPBVUHWMZVUAVVIWW
      TWWQVUAVVIWWTVHZWVMRHZSWVMOPWWQWXBWVLRHZWXCVURVUHABCWNZWVLUXHVNWXBSVYCWVM
      OVYDWXBSWVLWRPZVYFKZVYCWVMOPZWXBWXFVYFVURVUHABCWSWTXAWXBVYIVYIWXDVYAWXGWX
      HNVYIWXBXBXCZWXIWXEVYAWXBWPXCSSWVLMXDXEXFXGWVMXHXIXJXKXLWVAWWSVXNWVAWVMWV
      MIZWWSWVMXPWWRWXJGVUHVVPVWPVUHIZVWRWVMWVMWXKVWQWVLMWDVWPVUHVURAUTUXIVAUXJ
      UXKYIVWTWWSFWVMLVWOWVMIVWSWWRGVVPVWOWVMVWRXQVBYFXIWVRWVNUFWVMVXAVWKWVMVUS
      WRUXLUXMXRYJUXQXKYHTXNYLXLXLWUFWVAWVNWVKNZVXBWUEWVAWUDWXLWUEWVAKVWGWWTKZW
      UDWXLVWGVWBWVAWXMVWBWVAKZWWTVWGVVQVVSWVAWWTVWAWXAUXRZUXNXLVVJWWTWUDWXLVUF
      VVJWWTKZWUDKZWUIWVNWVJWXQWUIWVNWVJNWXQWUIKZWVEWVNWVHWXRWVEWVLWVDWDVLZWVMO
      PZWVNWVHNWXPWUIWVEWXTNWUDWXPWUIKZWVEWXTWYAWVDRHZVYAWXDWVEWXTYKVUAWWTWUIWY
      BVVIVUAWWTWUIWYBVUHWUHABCWNXJYMZVYAWYAWPXCWXPWXDWUIVUAVVIWWTWXDWXEXJZYNZW
      VDMWVLUXOVSUXPTWXRWXTWVNWVHWXRWXTWVNKZWXSVUSOPZWVHWXRWXSRHZWXCWWMWYFWYGNW
      XRWXDWYBWYHWXPWXDWUDWUIWYDYCWXPWUIWYBWUDWYCTWVLWVDWOXTZWXPWUIWXCWUDWYAWXD
      VYAWXCWYEWPWVLMWOWJTWUDWWMWXPWUIWWNYBZWXSWVMVUSUXSVSWXRWVGWXSWRPZWYGWVHWX
      PWUIWYKWUDVVJWUIWWTWYKVUAVVIWUIWWTWYKVUAVVIWUIWWTWYKVURWUHVUHABCUXTUYAUYB
      YOTWXRWVGRHZWYHWWMWYKWYGKWVHNWXPWUIWYLWUDVVJWUIWYLWWTVUAVVIWUIWYLVURWUHAB
      CWNXJTTWYIWYJWVGWXSVUSUYCVSUYDYPUYGYPYQYHYQYMXRYOTXFTUYEWUGWVAWVBWVFYKZWU
      PWUFWVAWYMVXBWUEWVAWYMWUDVWGVWBWVAWYMWXNVWGWWTWYMWXOVVJWWTWYMVUFVUAWWTWYM
      VVIVUAWWTVVKWYMUSWUHAVUHMBCYRUYFTTXKXLTTTWUFWUJWVIYKZVXBWUPWVAVWGWUDWYNVW
      BVVJWUDWYNVUFVUAVVIWUDWYNWUHAVURVUSBCYRXJTTVPYSYTUYHUYIXFXMYPWUFWUJWUINZV
      XBVWGWUDWYOVWBVVJWUDWYOVUFVVJWUDKVUTBWUHVUAVVIWUDVUTBVGAVURVUSBCUYJXJUYKT
      TYNUYLUYMYHYMUYNYTXFYHXMYLYMUYQUYRYHXMYTUYOEUAABDUBCUYPUCABUDCUYSYSUYT $.
      $( [15-Jan-2014] $) $( [2-Sep-2009] $)
  $}

