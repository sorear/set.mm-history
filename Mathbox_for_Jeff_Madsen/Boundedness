$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Boundedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TotBnd $.
  $c Bnd $.
  $( Extend class notation with the class of totally bounded metric spaces. $)
  ctotbnd $a class TotBnd $.
  $( Extend class notation with the class of bounded metric spaces. $)
  cbnd $a class Bnd $.

  ${
    $d m d v b x $.
    $( Define the class of totally bounded metrics.  A metric space is totally
       bounded iff it can be covered by a finite number of balls of any given
       radius. $)
    df-totbnd $a |- TotBnd = { m e. Met | A. d e. RR+ E. v e. Fin ( U. v =
      dom dom m /\ A. b e. v E. x e. dom dom m b = ( x ( ball ` m ) d ) ) } $.
  $}

  ${
    $d M m d v b x $.  $d X m d v b x $.
    istotbnd.1 $e |- X = dom dom M $.
    $( The predicate "is a totally bounded metric space".  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    istotbnd $p |- ( M e. TotBnd <-> ( M e. Met /\ A. d e. RR+ E. v e. Fin
        ( U. v = X /\ A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( vm cv cdm wceq cbl cfv co wrex wral wa cfn crp eqeq2d cuni ctotbnd dmeq
      cme dmeqd syl6eqr fveq2 opreqd rexeqbidv ralbidv anbi12d df-totbnd elrab2
      rexbidv ) BIZUAZHIZJZJZKZEIZAIZFIZUQLMZNZKZAUSOZEUOPZQZBROZFSPUPDKZVAVBVC
      CLMZNZKZADOZEUOPZQZBROZFSPHCUDUBUQCKZVJVRFSVSVIVQBRVSUTVKVHVPVSUSDUPVSUSC
      JZJDVSURVTUQCUCUEGUFZTVSVGVOEUOVSVFVNAUSDWAVSVEVMVAVSVDVLVBVCUQCLUGUHTUIU
      JUKUNUJABHEFULUM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M d v b x $.  $d X d v b x $.
    istotbnd2.1 $e |- X = dom dom M $.
    $( The predicate "is a totally bounded metric space."  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    istotbnd2 $p |- ( M e. Met -> ( M e. TotBnd <-> A. d e. RR+ E. v e. Fin
        ( U. v = X /\ A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( ctotbnd wcel cme cv cuni wceq cbl cfv co wrex wral wa cfn istotbnd baib
      crp ) CHICJIBKZLDMEKAKFKCNOPMADQEUDRSBTQFUCRABCDEFGUAUB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M d v b x $.
    $( The predicate "totally bounded" implies ` M ` is a metric space.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    totbndmet $p |- ( M e. TotBnd -> M e. Met ) $=
      ( vv vb vx vd ctotbnd wcel cme cv cuni cdm wceq cbl cfv wrex wral cfn crp
      co wa eqid istotbnd simplbi ) AFGAHGBIZJAKKZLCIDIEIAMNSLDUEOCUDPTBQOERPDB
      AUECEUEUAUBUC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X a b c d r s t u v w x y z $.  $d M a b c d r s t u v w x y z $.
    $d Y a b c d r s t u v w x y z $.  $d N a b c d r s t u v w x y z $.
    sstotbnd.1 $e |- X = dom dom M $.
    sstotbnd.2 $e |- N = ( M |` ( Y X. Y ) ) $.
    $( Condition for a subset of a metric space to be totally bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    sstotbnd $p |- ( ( M e. Met /\ Y C_ X ) -> ( N e. TotBnd <->
                        A. d e. RR+ E. v e. Fin ( Y C_ U. v /\
                          A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( vs vt vy wcel wss wa wceq wrex wral cfn vw vu vz va vc cme ctotbnd cuni
      vr cv cbl cfv co crp cdm wb cxp cres metres adantr syl5eqel istotbnd2 syl
      eqid w3a cvv simplr resexg dmexg ad2antrr simprr indexfi syl111anc wi cab
      3syl firnfi2 adantl ad2antlr metssba2 dmeqi syl6reqr eqeq2d biimpa eqcomd
      adantlr visset eqeq1 rexbidv elab biimpri ralimi dfss3 sylibr mpan2 oprex
      opreq1 rcla4ev cin simpr sseq2d ssel2 adantllr blssp anassrs a1i syl11anc
      an32s eqsstrd ex rexlimdva syl5bi ralrimiv adantrr adantll ssrexv cbvrexv
      adantlrr 3ad2antr1 unieq raleq anbi12d mpd c2 wne ineq1 neeq1d elrab jcad
      c0 sylan2 eluni2 wex elin syl6 imp rcla4cva ad2ant2rl eleq2 syld ad2antll
      uniss sylan syldan eqtrd inss1 sseq2 uniss2 sstrd biimpd imdistani impcom
      3adantr3 sstr2 syl6ib syl12anc ralimdva sylbid cdiv elrpii rpdivcl opreq2
      2re 2pos ralbidv anbi2d rcla4v crab inelcm expcom 3imtr4g ralimia 3imtr4i
      reximdv2 ssrab2 ssralv ax-mp ancom bitri exbii df-rex 3bitr4i biimpi rgen
      sylbi 3jca ssfi jctild 3anbi123d ssexg ancoms 3expb an12s a1d cc0 clt wbr
      n0 cr rpregt0 blssm syl2an eleq2d imbi12d mpbid sseq1 unissb sylib elequ1
      syl5ibrcom elequ2 syl5bb 3ad2antl2 adantlll ad2ant2lr biimpcd blhalf an4s
      id reximdv sseld expimpd ancomsd sylanr1 adantlrl simpll sylanbrc ancom2s
      3ad2antl1 adantrl eleqtrrd ad3antrrr eqtr4d biimpar jctird eqeq1d expdimp
      ssrdv eqssd com23 exp3a 3impd ralrimdva sylibrd impbid ) CUFNZFEOZPZDUGNZ
      FBUJZUHZOZGUJZAUJZHUJZCUKULZUMZQZAERZGVUJSZPZBTRZHUNSZVUHVUIUAUJZUHZDUOZU
      OZQZUBUJZKUJZVUODUKULZUMZQZKVVGRUBVVDSZPZUATRZHUNSZVVCVUHDUFNZVUIVVQUPVUH
      DCFFUQZURZUFJVUFVVTUFNVUGCFUSUTVAZKUADVVGUBHVVGVDZVBVCVUHVVPVVBHUNVUHVUOU
      NNZPZVVOVVBUATVWDVVDTNZPZVVOVVBVWFVVOPZUCUJZVVGOZVVMKVWHRZUBVVDSZVVMUBVVD
      RKVWHSZVEZUCTRZVVBVWGVWEVVGVFNZVVNVWNVWDVWEVVOVGVWDVWOVWEVVOVUFVWOVUGVWCV
      UFDVFNVVFVFNVWOVUFDVVTVFJCVVSUFVHVADVFVIVVFVFVIVPVJVJVWFVVHVVNVKVVMUBKVVD
      VVGVFUCVLVMVWFVVHVWNVVBVNVVNVWFVVHPVWMVVBUCTVWFVVHVWHTNZVWMVVBVNVWFVVHVWP
      PZPZVWMVVBVWRVWMPUDUJZLUJZVUOVUPUMZQZLVWHRZUDVOZTNZFVXDUHZOZVUSGVXDSZVVBV
      WQVXEVWFVWMVWPVXEVVHUDLVWHVXAVQVRVSVWRVWIVWKVXGVWLVWFVVHVWIVWKPZVXGVWPVWD
      VVHVXIVXGVWEVWDVVHPZVXIPFVVEVXFVXJFVVEQZVXIVUHVVHVXKVWCVUHVVHPVVEFVUHVVHV
      VEFQVUHVVGFVVEVUHFVVTUOZUOVVGCEFIVTVVFVXLDVVTJWAWAWBZWCWDWEWFUTVWDVXIVVEV
      XFOVVHVWDVXIPZVVEVUMVVLQZKVWHRZGVOZUHZVXFVXNVVDVXQOZVVEVXROVWKVXSVWDVWIVW
      KVVIVXQNZUBVVDSVXSVWJVXTUBVVDVXTVWJVXPVWJGVVIUBWGVUMVVIQVXOVVMKVWHVUMVVIV
      VLWHWIWJWKWLUBVVDVXQWMWNUUAVVDVXQUUBVCVXNVUNMUJZOZMVXDRZAVXQSZVXRVXFOVWDV
      WIVYDVWKVWDVWIPZVYCAVXQVUNVXQNVUNVVLQZKVWHRZVYEVYCVXPVYGGVUNAWGZVUMVUNQVX
      OVYFKVWHVUMVUNVVLWHWIWJVYEVYFVYCKVWHVYEVVJVWHNZPZVYFVYCVYJVYFPZVVJVUOVUPU
      MZVXDNZVUNVYLOZVYCVYIVYMVYEVYFVYIVYLVXAQZLVWHRZVYMVYIVYLVYLQZVYPVYLVDVYOV
      YQLVVJVWHVWTVVJQZVXAVYLVYLVWTVVJVUOVUPWQWCWRWOVXCVYPUDVYLVVJVUOVUPWPVWSVY
      LQVXBVYOLVWHVWSVYLVXAWHWIWJWNVSVYKVUNVYLFWSZVYLVYKVUNVVLVYSVYJVYFWTVYJVVL
      VYSQZVYFVYEVYIVVJFNZVYTVUHVWIVYIWUAVWCVUHVWIPZVWHFOZVYIWUAVUHVWIWUCVUHVVG
      FVWHVXMXAZWDVWHFVVJXBUUCXCVWDWUAVYTVWIVUHWUAVWCVYTVUHWUAVWCVYTVUOFCDEVVJI
      JXDXEXHWFUUDUTUUEVYSVYLOVYKVYLFUUFXFXIVYBVYNMVYLVXDVYAVYLVUNUUGWRXGXJXKXL
      XMXNAMVXQVXDUUHVCUUIWFXIXCXRUUMVWRVWKVWIVXHVWLVWFVWIVXHVWQVWDVWIVXHVWEVUH
      VWIVXHVWCWUBVUHWUCPVWHEOZVXHVUHVWIWUCVUHVWIWUCWUDUUJUUKVUGWUCWUEVUFWUCVUG
      WUEVWHFEUUNUULXOWUEVUSGVXDVUMVXDNVUMVXAQZLVWHRZWUEVUSVXCWUGUDVUMGWGVWSVUM
      QVXBWUFLVWHVWSVUMVXAWHWIWJWUEWUGWUFLERVUSWUFLVWHEXPWUFVURLAEVWTVUNQVXAVUQ
      VUMVWTVUNVUOVUPWQWCXQUUOXLXMVPWFWFWFXSVVAVXGVXHPBVXDTVUJVXDQZVULVXGVUTVXH
      WUHVUKVXFFVUJVXDXTXAVUSGVUJVXDYAYBWRUUPXJXEXKXNYCXJXKUUQUURVUHVVCVVHVVJVU
      OUEUJZVVKUMZQZHVVGRZKVVDSZPZUATRZUEUNSZVUIVUHVVCWUOUEUNVUHWUIUNNZPZVVCVUL
      VUMVUNWUIYDUUSUMZVUPUMZQZAERZGVUJSZPZBTRZWUOWUQVVCWVEVNZVUHWUQWUSUNNZWVFW
      UQYDUNNWVGYDUVCUVDUUTWUIYDUVAWOVVBWVEHWUSUNVUOWUSQZVVAWVDBTWVHVUTWVCVULWV
      HVUSWVBGVUJWVHVURWVAAEWVHVUQWUTVUMVUOWUSVUNVUPUVBWCWIUVEUVFWIUVGVCVRWURWV
      DWUOBTWURVUJTNZPZWVDFVVIUHZOZWVBGVVISZVYAVWTNZMFRZLVVISZVEZUBTRZWUOWVJWVD
      VWSFWSZYJYEZUDVUJUVHZTNZFWWAUHZOZWVBGWWASZWVOLWWASZVEZPWVRWVJWVDWWGWWBWVD
      WWGVNWVJWVDWWDWWEWWFVULWWDWVCVYAVUKNZMFSVYAWWCNZMFSVULWWDWWHWWIMFVYAFNZVY
      AVUNNZAVUJRWWKAWWARWWHWWIWWJWWKWWKAVUJWWAWWJVUNVUJNZWWKPZVUNWWANZWWKWWMWW
      JWWNWWLWWKWWJWWNWWKWWJPWWLVUNFWSZYJYEZWWNVYAVUNFUVIWWNWWLWWPPWVTWWPUDVUNV
      UJVWSVUNQZWVSWWOYJVWSVUNFYFYGYHWKYKXEUVJWWMWWKVNWWJWWLWWKWTXFYIUVNAVYAVUJ
      YLAVYAWWAYLUVKUVLMFVUKWMMFWWCWMUVMUTWVCWWEVULWWAVUJOZWVCWWEVNWVTUDVUJUVOZ
      WVBGWWAVUJUVPUVQVRWWFWVDWVOLWWAVWTWWANVWTVUJNZVWTFWSZYJYEZPWVOWVTWXBUDVWT
      VUJVWSVWTQWVSWXAYJVWSVWTFYFYGYHWXBWVOWWTWXBWVOVYAWXANZMYMWWJWVNPZMYMWXBWV
      OWXCWXDMWXCWVNWWJPWXDVYAVWTFYNWVNWWJUVRUVSUVTMWXAUWRWVNMFUWAUWBUWCVRUWEUW
      DXFUWFXFWVIWWBWURWVIWWRWWBWWSVUJWWAUWGWOVRUWHWVQWWGUBWWATVVIWWAQZWVLWWDWV
      MWWEWVPWWFWXEWVKWWCFVVIWWAXTXAWVBGVVIWWAYAWVOLVVIWWAYAUWIWRYOWURWVRWUOVNW
      VIWURWVQWUOUBTWURVVITNZPZWVLWVMWVPWUOWXGWVLWVMWVPWUOVNWXGWVPWVLWVMPZWUOWU
      RWXFWVPWXHWUOVNZWURWXFWVPPZVUJFOZWVNMVUJRZLVVISZWVNLVVIRMVUJSZVEZBTRZWXIV
      UHWXJWXPVNZWUQVUHFVFNZWXQVUGVUFWXRVUFVUGEVFNWXRVUFECUOZUOZVFIVUFWXSVFNWXT
      VFNCUFVIWXSVFVIVCVAFEVFUWJYKUWKWXRWXJWXPWXFWXRWVPWXPWXFWXRWVPWXPWVNLMVVIF
      VFBVLUWLUWMXJVCUTWURWXOWXIBTVUHWUQWVIWXOWXIVNVUHWUQWVIPZPZWXOWXIWYBWXOPZW
      XHVWSVWHWUIVVKUMZQZUCVUJRZUDVOZTNZWYGUHZVVGQZWULKWYGSZPZPWUOWYCWXHWYHWYLW
      YAWXHWYHVNZVUHWXOWVIWYMWUQWVIWYHWXHUDUCVUJWYDVQUWNVRVSWYCWXHWYJWYKWYCWXHW
      YJWYCWXHPZWYIFVVGWYNWYIFWYCWYIFOZWXHWYBWXMWXKWYOWXNVUHWUQWXKWYOWVIWURWXKP
      ZVUNFOZAWYGSWYOWYPWYQAWYGVUNWYGNVUNWYDQZUCVUJRZWYPWYQWYFWYSUDVUNVYHWWQWYE
      WYRUCVUJVWSVUNWYDWHWIWJWYPWYRWYQUCVUJWYPVWHVUJNZPWYQWYRWYDFOZWURWXKWYTXUA
      WXKWYTPWURVWHFNZXUAVUJFVWHXBWURXUBXUAWURVWHVVGNZWYDVVGOZVNZXUBXUAVNZVUHVV
      RWUIUWSNUWOWUIUWPUWQPZXUEWUQVWAWUIUWTVVRXUGPXUCXUDVVRXUCXUGXUDDVWHWUIVVGV
      WBUXAXHXJUXBVUHXUEXUFUPWUQVUHXUCXUBXUDXUAVUHVVGFVWHVXMUXCVUHVVGFWYDVXMXAU
      XDUTUXEYPYKXEVUNWYDFUXFUXJXKXLXMAWYGFUXGWNXRXSUTWYNUAFWYIWYNVVDFNZVVDWYIN
      ZWYNXUHPZVVDVUONZHWYGRZXUIXUJVVDVVJNZKVVIRZXULWXHXUHXUNWYCWVLXUHXUNWVMWVL
      XUHPVVDWVKNXUNFWVKVVDXBKVVDVVIYLUXHWFXOXUJXUMXULKVVIWYNXUHVVJVVINZXUMXULV
      NWYNXUHXUOPZPZXUMXULXUQXUMPZUIUJZVVJNZUIVUJRZXULXUQXVAXUMWXOWXHXUPXVAWYBW
      XOXUOXVAWXHXUHWXMWXKXUOXVAWXNWXLXVALVVJVVIWXLXUSVWTNZUIVUJRVYRXVAWVNXVBMU
      IVUJMUILUXIXQVYRXVBXUTUIVUJLKUIUXKWIUXLYQUXMYRUXNUTXURXUTXULUIVUJXUQXUMXU
      SVUJNZXUTXULVNZWYNXUPXUMXVCPZXVDWYNXUPXVEPZPZXUTXULXVGXUTPZXUSWUIVVKUMZWY
      GNZVVDXVINZXULXVFXUTXVJWYNXVEXVJXUPXUTXVCXVJXUMXVCXVIWYDQZUCVUJRZXVJXVCXV
      IXVIQZXVMXVIVDXVLXVNUCXUSVUJVWHXUSQWYDXVIXVIVWHXUSWUIVVKWQWCWRWOWYFXVMUDX
      VIXUSWUIVVKWPVWSXVIQWYEXVLUCVUJVWSXVIWYDWHWIWJWNVRVSXOXVHVVDXUSWUIVUPUMZF
      WSZXVIXVHVVDXVONZXUHVVDXVPNWYNXVFXUTXVQWYBWXHXVFXUTPZXVQWXOWYBWVMXVRXVQWV
      LXVFWYBWVMPXUOXUMPZXUTXVQXUOXUMXVSXUHXVCXVSUXSUXOWYBWVMXVSXUTPZXVQWVMXVTP
      ZWYBVVDWUTNZXUSWUTNZPZAERZXVQXWAVVJWUTQZAERZXWEWVMXVSXWGXUTWVMXUOXWGXUMWV
      BXWGGVVJVVIVUMVVJQWVAXWFAEVUMVVJWUTWHWIYQXNXNXVTXWGXWEVNZWVMXUMXUTXWHXUOX
      UMXUTPZXWFXWDAEXWFXWIXWDXWFXUMXWBXUTXWCVVJWUTVVDYSVVJWUTXUSYSYBUXPUXTXOVR
      YCVUHWUQXWEXVQWVIWURXWEXVQWURXWDXVQAEWURVUNENZPXWCXWBXVQVUFWUQXWJXWCXWBPX
      VQVNVUGVUFWUQPZXWJPXWCXWBXVQXWKXWJXWCXWBXVQVNXWKXWJXWCPPWUTXVOVVDVUFXWJWU
      QXWCWUTXVOOWUICEVUNXUSIUXQUXRUYAXEUYBXCUYCXKYPXRYKXEUYDUYEXCXEXVFXUHWYNXU
      TXUHXUOXVEUYFVSVVDXVOFYNUYGXVGXVIXVPQZXUTWYNXVEXWLXUPWYCXVCXWLWXHXUMWYBWX
      OXVCXWLWXOXVCPWYBXUSFNZXWLWXKWXMXVCXWMWXNVUJFXUSXBUYIVUHWUQXWMXWLWVIVUHWU
      QXWMXWLVUHXWMWUQXWLWUIFCDEXUSIJXDUYHXEXRYKXEYRUYJUTUYKXUKXVKHXVIWYGVUOXVI
      VVDYSWRXGXJXEXEXKYCXJXEXKYCHVVDWYGYLWNXJUYRUYSVUHVVGFQWYAWXOWXHVXMUYLUYMX
      JWYBWXMWXKWYKWXNVUHWXKWYKWYAVUHWXKPZWULKWYGVVJWYGNVVJWYDQZUCVUJRZXWNWULWY
      FXWPUDVVJKWGVWSVVJQWYEXWOUCVUJVWSVVJWYDWHWIWJXWPWUKHVUJRZXWNWULXWOWUKUCHV
      UJVWHVUOQWYDWUJVVJVWHVUOWUIVVKWQWCXQXWNVUJVVGOZXWQWULVNVUHXWRWXKVUHVVGFVU
      JVXMXAUYNWUKHVUJVVGXPVCXLXLXMWFXSUYOYIWUNWYLUAWYGTVVDWYGQZVVHWYJWUMWYKXWS
      VVEWYIVVGVVDWYGXTUYPWULKVVDWYGYAYBWRYOXJXEXKYTUYQUYTVUAVUBXKUTYTXKYTVUCVU
      HVVRVUIWUPUPVWAHUADVVGKUEVWBVBVCVUDVUE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M n p q r s t u v w x y z $.  $d X n p q r s t u v w x y z $.
    $d S n p q r s t u v w x y z $.
    totbndss.1 $e |- X = dom dom M $.
    $( A subset of a totally bounded metric space is totally bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    totbndss $p |- ( ( M e. Met /\ M e. TotBnd /\ S C_ X ) ->
                                      ( M |` ( S X. S ) ) e. TotBnd ) $=
      ( vx vw vy vu vt wcel wss wi wa cv wceq wrex wral cfn cvv ex vv vz vq cme
      vr vp vn vs ctotbnd cxp cres cuni cbl cfv co crp cdm cdiv 2re 2pos elrpii
      c2 rpdivcl opreq2 eqeq2d rexbidv ralbidv anbi2d rcla4v syl adantl cpw cin
      mpan2 c0 wne crab w3a ssrab2 ssfi ad2antlr dmexg syl5eqel ssexg ad3antrrr
      mpan9 pwexg ineq1 neeq1d elrab wex eqeq1 rcla4cva adantll inss1 syl5ibcom
      n0 sseli eleq2 blhalf inss2 oprex inex1 elpw mpbir a1i biimparc 3ad2antl2
      sseq1 ssrin eqidd opreq1 ineq1d rcla4ev syl11anc 3ad2ant3 adantr ad2antrr
      sseq2 anbi12d an4s com23 anassrs rexlimdva exlimdv sylan2 sylan2b adantlr
      imp mpdd biimprd ancoms syl6ibr syld jcad sylibd eqid reximdva istotbnd2
      wb syl12anc 3exp1 mpd exp3a an32s ralrimiva indexfi syl111anc uniss unipw
      anasss syl6sseq 3ad2ant1 ssel eluni syl6ib ad2antrl inelcm biimpri sseq1d
      an13s anbi1d rcla4cv syl5 expdimp elin ssel2 sylan2br a1d expimpd reximdv
      eluni2 adantrr ssrdv adantllr 3adantr3 eqss blssp ancom2s rexeqdv adantld
      metssba2 ralimdva impr 3adantr2 ralrimdva metres 3imtr4d 3imp ) BUDJZBUIJ
      ZACKZBAAUJUKZUIJZUWJUWLUWKUWNUWJUWLUWKUWNLUWJUWLMZUANZULZCOZUBNZENZUCNZBU
      MUNZUOZOZECPZUBUWPQZMZUARPZUCUPQZFNZULZUWMUQUQZOZGNZHNZUENZUWMUMUNUOZOZHU
      XLPZGUXJQZMZFRPZUEUPQZUWKUWNUWOUXIUYBUEUPUWOUXPUPJZMZUXIUWRUWSUWTUXPVBURU
      OZUXBUOZOZECPZUBUWPQZMZUARPZUYBUYDUXIUYLLZUWOUYDUYFUPJZUYMUYDVBUPJUYNVBUS
      UTVAUXPVBVCVNUXHUYLUCUYFUPUXAUYFOZUXGUYKUARUYOUXFUYJUWRUYOUXEUYIUBUWPUYOU
      XDUYHECUYOUXCUYGUWSUXAUYFUWTUXBVDVEVFVGVHVFVIVJVKUYEUYKUYBUARUYEUWPRJZMZU
      YKUXJAVLZKZINZAVMZUXNKZUXNUXOUXPUXBUOZAVMZOZHAPZMZGUXJPZIUFNZAVMZVOVPZUFU
      WPVQZQZVUGIVULPZGUXJQZVRZFRPZUYBUYQUYKVUQUYQUYKMZVULRJZUYRSJZVUGGUYRPZIVU
      LQZVUQUYPVUSUYEUYKUYPVULUWPKVUSVUKUFUWPVSUWPVULVTVNWAUWOVUTUYDUYPUYKUWOAS
      JZVUTUWJCSJZUWLVVCUWJCBUQZUQZSDUWJVVESJVVFSJBUDWBVVESWBVJWCUWLVVDVVCACSWD
      TWFASWGVJWEUYEUYKVVBUYPUYEUYKMZVVAIVULUYTVULJZVVGUYTUWPJZVUAVOVPZMVVAVUKV
      VJUFUYTUWPVUIUYTOVUJVUAVOVUIUYTAWHWIWJVVGVVIVVJVVAVVJVVGVVIMZUXJVUAJZFWKZ
      VVAFVUAWQVVKVVMVVAUYEUYKVVIVVMVVALZUYKVVIMUYEUYTUYGOZECPZVVNUYJVVIVVPUWRU
      YIVVPUBUYTUWPUWSUYTOUYHVVOECUWSUYTUYGWLVFWMWNUYEVVPMZVVLVVAFVVQVVLVVAUYEV
      VLVVPVVAUYEVVLMZVVPVVAVVRVVOVVAECVVRUWTCJZMZVVOUXJUYGJZVVAVVLVVOVWALUYEVV
      SVVLUXJUYTJVVOVWAVUAUYTUXJUYTAWOWRUYTUYGUXJWSWPWAVVTVWAVVOVVAVVRVVSVWAVVO
      VVALZLZUWOUYDVVLVVSVWCLZUWJUYDUWLVVLVWDUWJUYDMZUWLVVLMZMVVSVWAVWBVWEVWFVV
      SVWAMZVWBLVWEVWGVWFVWBVWEVWGVWFVWBLZUWJVVSUYDVWAVWHUWJVVSMUYDVWAMZMUYGUXJ
      UXPUXBUOZKZVWHUXPBCUWTUXJDWTUWJVWKVWHLVVSVWIUWJVWKVWFVVOVVAUWJVWKVWFVRZVV
      OMZVWJAVMZUYRJZVUAVWNKZVWNVUDOZHAPZVVAVWOVWMVWOVWNAKVWJAXAVWNAVWJAUXJUXPU
      XBXBXCXDXEXFVWKUWJVVOVWPVWFVWKVVOMUYTVWJKZVWPVVOVWSVWKUYTUYGVWJXIXGUYTVWJ
      AXJVJXHVWLVWRVVOVWFUWJVWRVWKVVLVWRUWLVVLUXJAJVWNVWNOZVWRVUAAUXJUYTAXAWRVV
      LVWNXKVWQVWTHUXJAUXOUXJOZVUDVWNVWNVXAVUCVWJAUXOUXJUXPUXBXLXMVEXNXOVKXPXQV
      UGVWPVWRMGVWNUYRUXNVWNOZVUBVWPVUFVWRUXNVWNVUAXSVXBVUEVWQHAUXNVWNVUDWLVFXT
      XNUUAUUBXRUUCYATYBYIUUDYAYCYIYBYJYDYIUUETYEYFYCYIYGUUKYGUUFYHVUGIGVULUYRS
      FUUGUUHTUYQUYKVUQUYBLVURVUPUYAFRVURUXJRJZMZVUPUXMUXTVXDVUPUXKAOZUXMVXDVUP
      UXKAKZAUXKKZMVXEVXDVUPVXFVXGVUPVXFLVXDUYSVUMVXFVUOUYSUXKUYRULAUXJUYRUUIAU
      UJUULUUMXFVXDVUPVXGVXDUYSVUMVXGVUOVURUYSVUMMZVXGVXCUYEUYKVXHVXGUYPVVGVXHM
      ZUGAUXKVXIUGNZAJZVXJCJZVXJUXKJZUYEVXKVXLLZUYKVXHUWLVXNUWJUYDACVXJUUNWAXRV
      XIVXKVXLVXMLVXIVXKMZVXLVXJUHNZJZVXPUWPJZMZUHWKZVXMVVGVXLVXTLZVXHVXKUWRVYA
      UYEUYJUWRVXLVXJUWQJZVXTUWRVYBVXLUWQCVXJWSYKUHVXJUWPUUOUUPUUQXRVXOVXSVXMUH
      VXOVXSVXPAVMZUXNKZVUFMZGUXJPZVXMVXHVXKVXSVYFLZVVGVUMVXKVYGUYSVUMVXKVXSVYF
      VXKVXSMVXPVULJZVUMVYFVXRVXQVXKVYHVXQVXKMZVXRVYCVOVPZVYHVXJVXPAUURVYHVXRVY
      JMVUKVYJUFVXPUWPVUIVXPOVUJVYCVOVUIVXPAWHWIWJUUSYFUVAVUHVYFIVXPVULUYTVXPOZ
      VUGVYEGUXJVYKVUBVYDVUFVYKVUAVYCUXNUYTVXPAWHUUTUVBVFUVCUVDUVEWNWNVXKVXSVYF
      VXMLZLVXIVXKVXSVYLVXKVXQVYLVXRVXQVXKVYLVYIVYFVXJUXNJZGUXJPVXMVYIVYEVYMGUX
      JVYIVYDVUFVYMVYIVYDMVYMVUFVYDVYIVYMVYIVYDVXJVYCJVYMVXJVXPAUVFVYCUXNVXJUVG
      UVHYLUVIUVJUVKGVXJUXJUVLYMYLUVMTVKYJYEYNTYJUVNUVOYHUVPTYOUXKAUVQYMVXDAUXL
      UXKUYQAUXLOZUYKVXCUWOVYNUYDUYPBCADUWBZXRXRVEYPUYEVUPUXTLUYPUYKVXCUYEVUPUX
      TUYEUYSVUOUXTVUMUYEUYSVUOUXTUYEUYSMZVUNUXSGUXJVYPUXNUXJJZMZVUGUXSIVULVYRV
      UGUXSLVVHVYRVUFUXSVUBVYRVUFUXRHAPUXSVYRVUEUXRHAVYRUXOAJZMZUXRVUEVYTUXQVUD
      UXNVYPVYSUXQVUDOZVYQUYEVYSWUAUYSUWOUYDVYSWUAUWOVYSUYDWUAUXPABUWMCUXODUWMY
      QUVRUVSYCYHYHVEYKYRVYRUXRHAUXLUWOVYNUYDUYSVYQVYOWEUVTYPUWAXQYDUWCUWDUWETW
      EYOYRTYJYDYNUWFUWJUWKUXIYTUWLEUABCUBUCDYSXQUWJUWNUYCYTZUWLUWJUWMUDJWUBBAU
      WGHFUWMUXLGUEUXLYQYSVJXQUWHTYBUWI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d m x r $.
    $( Define the class of bounded metrics.  A metric space is bounded iff it
       can be covered by a single ball. $)
    df-bnd $a |- Bnd = { m e. Met | A. x e. dom dom m E. r e. RR+
                                          dom dom m = ( x ( ball ` m ) r ) } $.
  $}

  ${
    $d M m x r $.  $d X m x r $.
    isbnd.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isbnd $p |- ( M e. Bnd <-> ( M e. Met /\ A. x e. X E. r e. RR+
                            X = ( x ( ball ` M ) r ) ) ) $=
      ( vm cv cdm cbl cfv co wceq crp wrex wral cme cbnd dmeq dmeqd syl6eqr
      fveq2 opreqd eqeq12d rexbidv raleqbidv df-bnd elrab2 ) FGZHZHZAGZDGZUHIJZ
      KZLZDMNZAUJOCUKULBIJZKZLZDMNZACOFBPQUHBLZUPUTAUJCVAUJBHZHCVAUIVBUHBRSETZV
      AUOUSDMVAUJCUNURVCVAUMUQUKULUHBIUAUBUCUDUEAFDUFUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M m x r $.  $d X m x r $.
    isbnd2.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isbnd2 $p |- ( M e. Met -> ( M e. Bnd <->
                        A. x e. X E. r e. RR+ X = ( x ( ball ` M ) r ) ) ) $=
      ( cbnd wcel cme cv cbl cfv co wceq crp wrex wral isbnd baib ) BFGBHGCAIDI
      BJKLMDNOACPABCDEQR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x y r s $.  $d X x y r s $.
    isbnd3.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  Uses a single point instead
       of an arbitrary point in the space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isbnd3 $p |- ( ( M e. Bnd /\ X =/= (/) ) <-> ( M e. Met /\
                E. x e. X E. r e. RR+ X = ( x ( ball ` M ) r ) ) ) $=
      ( vy vs wcel wa cv co wceq crp wrex eqeq2d wi c2 wss a1i sylan2 cbnd wral
      c0 wne cme cbl cfv isbnd anbi1i anass r19.2z ancoms opreq1 rexbidv opreq2
      cbvrexv rexbii bitri cmul 2pos elrpii rpmulcl mpan ad2antll ad2antrr cdiv
      2re cc cc0 rpcn 2cn 2ne0 w3a divcan3 eqcomd syl111anc biimpd adantr simpr
      opreq2d imp eleq2 biimpac blhalf anasss anassrs eqsstrd syldan cr clt wbr
      expr elrp sylib blssm an32s adantlrl rcla4ev syl11anc ralrimdva rexlimdvv
      eqssd ex syl5bi rexn0 jcad impbid2 pm5.32i 3bitri ) BUAHZCUCUDZIBUEHZCAJZ
      DJZBUFUGZKZLZDMNZACUBZIZXKIXLXSXKIZIXLXRACNZIXJXTXKABCDEUHUIXLXSXKUJXLYAY
      BXLYAYBXKXSYBXRACUKULXLYBXSXKYBCFJZGJZXOKZLZGMNZFCNZXLXSYBCYCXNXOKZLZDMNZ
      FCNYHXRYKAFCXMYCLZXQYJDMYLXPYICXMYCXNXOUMOUNUPYKYGFCYJYFDGMXNYDLYIYECXNYD
      YCXOUOOUPUQURXLYFXSFGCMXLYCCHZYDMHZIZYFXSPXLYOIZYFXRACYPXMCHZIZYFXRYRYFIZ
      QYDUSKZMHZCXMYTXOKZLZXRYPUUAYQYFYNUUAXLYMQMHYNUUAQVGUTVAQYDVBVCZVDVEYSCUU
      BYRYFCYCYTQVFKZXOKZLZCUUBRYRYFUUGYPYFUUGPZYQYNUUHXLYMYNYFUUGYNYEUUFCYNYDU
      UEYCXOYNYDVHHZQVHHZQVIUDZYDUUELYDVJUUJYNVKSUUKYNVLSUUIUUJUUKVMUUEYDYDQVNV
      OVPVTOVQVDVRWAYRUUGICUUFUUBYRUUGVSYPYQUUGUUFUUBRZYQUUGIYPXMUUFHZUULUUGYQU
      UMCUUFXMWBWCYPUUMUULXLYMYNUUMUULPZYNXLYMIZUUAUUNUUDUUOUUAUUMUULYTBCYCXMEW
      DWLTWEWATWFWGWHYRUUBCRZYFXLYNYQUUPYMXLYQYNUUPYNXLYQIYTWIHVIYTWJWKIZUUPYNU
      UAUUQUUDYTWMWNBXMYTCEWOTWPWQVRXBXQUUCDYTMXNYTLXPUUBCXNYTXMXOUOOWRWSXCWTXC
      XAXDYBXKPXLXRACXESXFXGXHXI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x y r $.  $d X x y r $.  $d S x y r $.
    bndss.1 $e |- X = dom dom M $.
    $( A subset of a bounded metric space is bounded.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    bndss $p |- ( ( M e. Bnd /\ S C_ X ) -> ( M |` ( S X. S ) ) e. Bnd ) $=
      ( vy vr vx cme wcel cv cbl cfv co wceq crp wrex wral wa cin an32s wss cxp
      cres cdm metres ad2antrr metssba2 eleq2d biimpar adantllr wi ssel2 ancoms
      cbnd opreq1 eqeq2d rexbidv rcla4va sylan adantlll eqid blssp an4s anassrs
      adantr dfss biimpi incom ineq1 sylan9eq adantll adantlr 3eqtr2rd reximdva
      syl6eq ex imp syldan ralrimiva jca isbnd anbi1i 3imtr4i ) BHIZCEJZFJZBKLZ
      MZNZFOPZECQZRZACUAZRZBAAUBUCZHIZWOUDUDZGJZWFWOKLMZNZFOPZGWQQZRBUNIZWMRWOU
      NIWNWPXBWDWPWKWMBAUEUFWNXAGWQWNWRWQIZWRAIZXAWDWMXDXEWKWDWMRZXEXDXFAWQWRBC
      ADUGZUHUIUJWLXEWMXAWLXERWMXAWDXEWKWMXAUKWDXERZWKRWMXAXHWMWKXAXHWMRZWKCWRW
      FWGMZNZFOPZXAXEWMWKXLWDXEWMRWRCIZWKXLWMXEXMACWRULUMWJXLEWRCWEWRNZWIXKFOXN
      WHXJCWEWRWFWGUOUPUQURUSUTXIXLXAXIXKWTFOXIWFOIZRZXKWTXPXKRWSXJASZAWQXPWSXQ
      NZXKXHWMXOXRWDWMXEXOXRWFABWOCWRDWOVAVBVCVDVEXIXKAXQNZXOWMXKXSXHWMXKACASZX
      QWMAACSZXTWMAYANACVFVGACVHVOCXJAVIVJVKVLXIAWQNZXOXKWDWMYBXEXGVLUFVMVPVNVQ
      VRTVPTVQTVRVSVTXCWLWMEBCFDWAWBGWOWQFWQVAWAWC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x r $.  $d X x r $.  $d Y x r $.  $d R x r $.
    blbnd.1 $e |- X = dom dom M $.
    $( A ball is bounded.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    blbnd $p |- ( ( M e. Met /\ Y e. X /\ R e. RR+ ) ->
          ( M |` ( ( Y ( ball ` M ) R ) X. ( Y ( ball ` M ) R ) ) ) e. Bnd ) $=
      ( vx vr cme wcel crp cbl cfv co cdm cv wceq wrex wa sylan2b syl11anc cres
      w3a cxp cbnd c0 wne metres 3ad2ant1 cr cc0 clt wbr blcntr 3impa wss simp1
      elrp blssm metssba2 eleqtrd simp3 eqid blssp syl22anc inidm syl6eq eqtr2d
      cin opreq2 eqeq2d rcla4ev opreq1 rexbidv isbnd3 sylanbrc simpld ) BHIZDCI
      ZAJIZUBZBDABKLMZWAUCUAZUDIZWBNNZUEUFZVTWBHIZWDFOZGOZWBKLZMZPZGJQZFWDQZWCW
      ERVQVRWFVSBWAUGUHVTDWDIWDDWHWIMZPZGJQZWMVTDWAWDVQVRVSDWAIZVSVQVRRZAUIIUJA
      UKULRZWQAUQZBDACEUMSUNZVTVQWACUOZWAWDPVQVRVSUPZVQVRVSXBVSWRWSXBWTBDACEURS
      UNZBCWAEUSTZUTVTVSWDDAWIMZPZWPVQVRVSVAZVTXFWAWDVTXFWAWAVHZWAVTVQXBWQVSXFX
      IPXCXDXAXHAWABWBCDEWBVBVCVDWAVEVFXEVGWOXGGAJWHAPWNXFWDWHADWIVIVJVKTWLWPFD
      WDWGDPZWKWOGJXJWJWNWDWGDWHWIVLVJVMVKTFWBWDGWDVBVNVOVP $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M a b c d m r s t u v x y z $.  $d X a b c d m r s t u v x y z $.
    totbndbnd.1 $e |- X = dom dom M $.
    $( A totally bounded metric space is bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    totbndbnd $p |- ( M e. TotBnd -> M e. Bnd ) $=
      ( vb vx va vc wcel wceq wrex wa crp c1 wi clt wbr c0 cr cc0 adantlr vv vd
      vy vr vu vs vz vt cme ctotbnd cbnd totbndmet cv cuni cbl cfv wral cfn 1rp
      co opreq2 eqeq2d rexbidv ralbidv anbi2d rcla4v ax-mp wss w3a simplr dmexg
      cvv cdm syl5eqel ad3antrrr simprr indexfi syl111anc caddc crab cdom copab
      syl wne crn cab ssel2 metcl 1re readdcl sylancl addid1i metge0 lt01 jctir
      0cn cle 0re a1i leltadd syl22anc syl5eqbrr elrp sylanbrc 3expa syl5ibrcom
      mpd eleq1 sylan2 anassrs rexlimdva 3ad2antr1 eqid syl11anc wn eqeq1 sylan
      mp2 wor eleq2 ad2antlr ad2antrr ax-17 hban elrab rpre ex adantl anasss wb
      imp adantllr adantr an32s syld com23 elbl sylan2b 3imtr4d syl5 abbidv wfn
      pm4.71rd rnopab2 df-rab 3eqtr4g visset fnopab2 fnrndomg syl6eqbrr dmopab2
      oprex domfi eqeq1i dm0rn0 bitr3i rex0 rexeq mtbiri ne0i unieq uni0 syl6eq
      syl5ib necon3d mpan9 r19.2z eqidd reximi rexlimivw nsyl necon2ai adantlll
      sylbir 3ad2antr2 ssrab2 rpssre sstri ltso soss fimax ssrexv eluni2 biimpi
      eqnetrrd syl6bir 3ad2antl2 adantll hbre1 hbral hbra1 hb3an biimpa simplbi
      rcla4cva equcom eqle syl5bi necon3bd syl2an ancoms imim12d pm2.61 sylsyld
      ltle ralimdva anim1i peano2re opreq1d rcla4ev mpan2 breq1 rcla4va ancom2s
      idd 3ad2antl1 anim2i ltadd2 biimpd ltletr mettri 3exp2 imp41 lelttr mpand
      exp3a imdistand mpanr12 exp4b rexlimd blssm sseld eqrdv reximdva adantlrr
      impbid mpdan ralrimdva istotbnd2 isbnd2 mpcom ) AUIHZAUJHZAUKHZAULVUBUAUM
      ZUNZBIZDUMZEUMZUBUMZAUOUPZUTZIZEBJZDVUEUQZKZUAURJZUBLUQZBUCUMZUDUMZVUKUTZ
      IZUDLJZUCBUQVUCVUDVUBVURVVCUCBVURVUGVUHVUIMVUKUTZIZEBJZDVUEUQZKZUAURJZVUB
      VUSBHZKZVVCMLHVURVVINUSVUQVVIUBMLVUJMIZVUPVVHUAURVVLVUOVVGVUGVVLVUNVVFDVU
      EVVLVUMVVEEBVVLVULVVDVUHVUJMVUIVUKVAVBVCVDVEVCVFVGVVKVVHVVCUAURVVKVUEURHZ
      KZVVHVVCVVNVVHKZUEUMZBVHZVVEEVVPJZDVUEUQZVVEDVUEJZEVVPUQZVIZUEURJZVVCVVOV
      VMBVLHZVVGVWCVVKVVMVVHVJVUBVWDVVJVVMVVHVUBBAVMZVMZVLCVUBVWEVLHVWFVLHAUIVK
      VWEVLVKWCVNVOVVNVUGVVGVPVVEDEVUEBVLUEVQVRVVNVUGVWCVVCVVGVVKVUGVWCVVCVVMVV
      KVUGKZVWCVVCVWGVWBVVCUEURVWGVVPURHZKZVWBVVCVWIVWBKZVUTUFUMZWDZVWKVUTOPZNZ
      UFFUMZVUSGUMZAUTZMVSUTZIZGVVPJZFLVTZUQZUDVXAJZVVCVWJVXAURHZVXAQWDVXCVWJVW
      HVXAVVPWAPVXDVWGVWHVWBVJVWJVXAVWPVVPHZVWSKGFWBZWEZVVPWAVWJVWTFWFZVWOLHZVW
      TKZFWFZVXGVXAVWIVVSVVQVXHVXKIZVWAVWGVVQVXLVWHVVKVVQVXLVUGVVKVVQKZVWTVXJFV
      XMVWTVXIVXMVWSVXIGVVPVVKVVQVXEVWSVXINZVVQVXEKVVKVWPBHZVXNVVPBVWPWGVVKVXOK
      VXIVWSVWRLHZVUBVVJVXOVXPVUBVVJVXOVIZVWRRHZSVWROPVXPVXQVWQRHZMRHZVXRVUSVWP
      ABCWHZWIVWQMWJWKVXQSSSVSUTZVWROSWPWLZVXQSVWQWQPZSMOPZKZVYBVWROPZVXQVYDVYE
      VUSVWPABCWMWNWOVXQSRHZVYHVXSVXTVYFVYGNVYHVXQWRWSZVYIVYAVXTVXQWIWSSSVWQMWT
      XAXGXBVWRXCXDXEVWOVWRLXHXFXIXJXKUUCUUATTXLGFVVPVWRUUDVWTFLUUEUUFZVVPVLHVX
      FVVPUUBVXGVVPWAPUEUUGGFVVPVWRVXFVWQMVSUULZVXFXMZUUHVVPVLVXFUUIXRUUJVVPVXA
      UUMXNVWJVXGVXAQVYJVWIVVQVVSVXGQWDZVWAVWGVVSVYMVWHVVJVUGVVSVYMVUBVVJVUGKZV
      VSKZVXGQVXGQIZVVPQIZVYOXOVYQVXFVMZQIVYPVYRVVPQGFVVPVWRVXFVYKVYLUUKUUNVXFU
      UOUUPVYQVUIVUIIZEVVPJZVYOVYQVYTVYSEQJVYSEUUQVYSEVVPQUURUUSVYNVUEQWDZVVSVY
      TVVJBQWDVUGWUABVUSUUTVUGVUEQBQVUEQIZVUFQIVUGBQIWUBVUFQUNQVUEQUVAUVBUVCVUF
      BQXPUVDUVEUVFWUAVVSKVVRDVUEJVYTVVRDVUEUVGVVRVYTDVUEVVEVYSEVVPVVEVUIUVHUVI
      UVJWCXQUVKUVNUVLUVMTUVOUWEUDUFVXAOVXARVHROXSVXAOXSVXALRVWTFLUVPZUVQUVRUVS
      VXAROUVTXRUWAXNVXCVXBUDLJZVWJVVCVXALVHVXCWUDNWUCVXBUDVXALUWBVGVWJVXBVVBUD
      LVWGVWBVUTLHZVXBVVBNVWHVWGVWBKZWUEKZVXBVVBWUGVXBKZUGBVVAWUHUGUMZBHZWUIVVA
      HZWUHWUJWUIUHUMZHZUHVUEJZWUKWUFWUJWUNNZWUEVXBVUGWUOVVKVWBVUGWUJWUIVUFHZWU
      NVUFBWUIXTWUPWUNUHWUIVUEUWCUWDUWFYAYBWUHWUMWUKUHVUEWUHWULVUEHZKZWULVVDIZE
      VVPJZWUMWUKNZWUGWUQWUTVXBWUFWUQWUTWUEVWBWUQWUTVWGVVSVVQWUQWUTVWAVVRWUTDWU
      LVUEVUHWULIVVEWUSEVVPVUHWULVVDXPVCUWOUWGUWHTTWURWUSWVAEVVPWUHWUQEWUGVXBEW
      UFWUEEVWGVWBEVWGEYCVVQVVSVWAEVVQEYCVVREDVUEVUHVUEHEYCVVEEVVPUWIUWJVVTEVVP
      UWKUWLYDWUEEYCYDVXBEYCYDWUQEYCYDWVAEYCWURVUIVVPHZWUSWUMWUKWUSWUMKWUIVVDHZ
      WURWVBKZWUKWUSWUMWVCWULVVDWUIXTUWMWVDWUJVUIWUIAUTZMOPZKZWUJVUSWUIAUTZVUTO
      PZKZWVCWUKWVDWUJWVFWVIWUHWVBWUJWVFWVINZNZWUQWUHWVBKVUSVUIAUTZMVSUTZVUTWQP
      ZWVLWUGVXBWVBWVOWUFWUEVXBWVBKZWVOWUFWUEWVPKZWVOVWGVVSVVQWVQWVONZVWAVVKVVQ
      WVRVUGVXMWVQWVOWVQVXMVWKVUTWQPZUFVXAUQZWVBKZWVOWUEVXBWVBWWAWUEVXBKWVTWVBW
      UEVXBWVTWUEVWNWVSUFVXAVWKVXAHZWUEVWKLHZVWNWVSNWWBWWCVWKVWRIZGVVPJZVWTWWEF
      VWKLVWOVWKIVWSWWDGVVPVWOVWKVWRXPVCYEUWNWUEWWCKZVUTVWKIZWVSNZVWNWWGXOZWVSN
      WVSWWCWWHWUEWWCVWKRHZWWHVWKYFZWWGVWKVUTIZWWJWVSUDUFUWPWWJWWLWVSVWKVUTUWQY
      GUWRWCYHWWFWWIVWLVWMWVSWWFWWGVUTVWKWWFWWGUXOUWSWWCWUEVWMWVSNZWWCWWJVUTRHZ
      WWMWUEWWKVUTYFZVWKVUTUXEUWTUXAUXBWWGWVSUXCUXDXIUXFYKUXGYIVXMWVBWVTWVOVXMW
      VBWVTWVOVXMWVBKZWVNVXAHZWVTWVOWWPWVNLHZWVNVWRIZGVVPJZWWQVVKVVQWVBWWRVVQWV
      BKVVKVUIBHZWWRVVPBVUIWGZVUBVVJWXAWWRVUBVVJWXAVIZWVNRHZSWVNOPWWRWXCWVMRHZW
      XDVUSVUIABCWHZWVMUXHWCWXCSVYBWVNOVYCWXCSWVMWQPZVYEKZVYBWVNOPZWXCWXGVYEVUS
      VUIABCWMWNWOWXCVYHVYHWXEVXTWXHWXINVYHWXCWRWSZWXJWXFVXTWXCWIWSSSWVMMWTXAXG
      XBWVNXCXDXEXIXJWVBWWTVXMWVBWVNWVNIZWWTWVNXMWWSWXKGVUIVVPVWPVUIIZVWRWVNWVN
      WXLVWQWVMMVSVWPVUIVUSAVAUXIVBUXJUXKYHVWTWWTFWVNLVWOWVNIVWSWWSGVVPVWOWVNVW
      RXPVCYEXDWVSWVOUFWVNVXAVWKWVNVUTWQUXLUXMXQYIUXNXIYGTXLYKXJXJWUGWVBWVOWVLN
      ZVXBWUFWVBWUEWXMWUFWVBKVWGWXAKZWUEWXMVWGVWBWVBWXNVWBWVBKZWXAVWGVVQVVSWVBW
      XAVWAWXBUXPZUXQXJVVKWXAWUEWXMVUGVVKWXAKZWUEKZWUJWVOWVKWXRWUJWVOWVKNWXRWUJ
      KZWVFWVOWVIWXSWVFWVMWVEVSUTZWVNOPZWVOWVINWXQWUJWVFWYANWUEWXQWUJKZWVFWYAWY
      BWVERHZVXTWXEWVFWYAYJVUBWXAWUJWYCVVJVUBWXAWUJWYCVUIWUIABCWHXEYLZVXTWYBWIW
      SWXQWXEWUJVUBVVJWXAWXEWXFXEZYMZWVEMWVMUXRVRUXSTWXSWYAWVOWVIWXSWYAWVOKZWXT
      VUTOPZWVIWXSWXTRHZWXDWWNWYGWYHNWXSWXEWYCWYIWXQWXEWUEWUJWYEYBWXQWUJWYCWUEW
      YDTWVMWVEWJXNZWXQWUJWXDWUEWYBWXEVXTWXDWYFWIWVMMWJWKTWUEWWNWXQWUJWWOYAZWXT
      WVNVUTUXTVRWXSWVHWXTWQPZWYHWVIWXQWUJWYLWUEVVKWUJWXAWYLVUBVVJWUJWXAWYLVUBV
      VJWUJWXAWYLVUSWUIVUIABCUYAUYBUYCYNTWXSWVHRHZWYIWWNWYLWYHKWVINWXQWUJWYMWUE
      VVKWUJWYMWXAVUBVVJWUJWYMVUSWUIABCWHXETTWYJWYKWVHWXTVUTUYDVRUYEYOUYFYOYPYG
      YPYLXQYNTXGTUYGWUHWVBWVCWVGYJZWUQWUGWVBWYNVXBWUFWVBWYNWUEVWGVWBWVBWYNWXOV
      WGWXAWYNWXPVVKWXAWYNVUGVUBWXAWYNVVJVUBWXAKVXTVYEWYNWIWNWUIAVUIMBCYQUYHTTX
      IXJTTTWUGWUKWVJYJZVXBWUQWVBVWGWUEWYOVWBVVKWUEWYOVUGWUEVVKWWNSVUTOPKZWYOVU
      TXCZWUIAVUSVUTBCYQYRTTVOYSYTUYIUYJXGXKYOWUGWUKWUJNZVXBVWGWUEWYRVWBVVKWUEW
      YRVUGVVKWUEKVVABWUIWUEVVKWYPVVABVHWYQAVUSVUTBCUYKYRUYLTTYMUYPUYMYGYLUYNYT
      XGYGXKYKYLUYOUYQYGXKYTUYREUAABDUBCUYSUCABUDCUYTYSVUA $.
      $( [2-Sep-2009] $)
  $}

