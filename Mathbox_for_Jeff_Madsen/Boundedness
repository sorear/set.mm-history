$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Boundedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TotBnd $.
  $c Bnd $.
  $( Extend class notation with the class of totally bounded metric spaces. $)
  ctotbnd $a class TotBnd $.
  $( Extend class notation with the class of bounded metric spaces. $)
  cbnd $a class Bnd $.

  ${
    $d m d v b x $.
    $( Define the class of totally bounded metrics.  A metric space is totally
       bounded iff it can be covered by a finite number of balls of any given
       radius. $)
    df-totbnd $a |- TotBnd = { m e. Met | A. d e. RR+ E. v e. Fin ( U. v =
      dom dom m /\ A. b e. v E. x e. dom dom m b = ( x ( ball ` m ) d ) ) } $.
  $}

  ${
    $d M m d v b x $.  $d X m d v b x $.
    istotbnd.1 $e |- X = dom dom M $.
    $( The predicate "is a totally bounded metric space". $)
    istotbnd $p |- ( M e. TotBnd <-> ( M e. Met /\ A. d e. RR+ E. v e. Fin
        ( U. v = X /\ A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( vm cv cuni cdm wceq cbl cfv co wrex wral wa cfn crp cme ctotbnd dmeq
      dmeqd syl6eqr eqeq2d fveq2 opreqd rexeqbidv ralbidv anbi12d rexbidv
      df-totbnd elrab2 ) BIZJZHIZKZKZLZEIZAIZFIZUQMNZOZLZAUSPZEUOQZRZBSPZFTQUPD
      LZVAVBVCCMNZOZLZADPZEUOQZRZBSPZFTQHCUAUBUQCLZVJVRFTVSVIVQBSVSUTVKVHVPVSUS
      DUPVSUSCKZKDVSURVTUQCUCUDGUEZUFVSVGVOEUOVSVFVNAUSDWAVSVEVMVAVSVDVLVBVCUQC
      MUGUHUFUIUJUKULUJABHEFUMUN $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M d v b x $.  $d X d v b x $.
    istotbnd2.1 $e |- X = dom dom M $.
    $( The predicate "is a totally bounded metric space." $)
    istotbnd2 $p |- ( M e. Met -> ( M e. TotBnd <-> A. d e. RR+ E. v e. Fin
        ( U. v = X /\ A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( ctotbnd wcel cme cv cuni wceq cbl cfv co wrex wral wa cfn crp istotbnd
      baib ) CHICJIBKZLDMEKAKFKCNOPMADQEUDRSBTQFUARABCDEFGUBUC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M d v b x $.
    $( The predicate "totally bounded" implies ` M ` is a metric space. $)
    totbndmet $p |- ( M e. TotBnd -> M e. Met ) $=
      ( vv vb vx vd ctotbnd wcel cme cv cuni cdm wceq cbl cfv co wrex wral wa
      cfn crp eqid istotbnd simplbi ) AFGAHGBIZJAKKZLCIDIEIAMNOLDUEPCUDQRBSPETQ
      DBAUECEUEUAUBUC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X a b c d r s t u v w x y z $.  $d M a b c d r s t u v w x y z $.
    $d Y a b c d r s t u v w x y z $.  $d N a b c d r s t u v w x y z $.
    sstotbnd.1 $e |- X = dom dom M $.
    sstotbnd.2 $e |- N = ( M |` ( Y X. Y ) ) $.
    $( Condition for a subset of a metric space to be totally bounded. $)
    sstotbnd $p |- ( ( M e. Met /\ Y C_ X ) -> ( N e. TotBnd <->
                        A. d e. RR+ E. v e. Fin ( Y C_ U. v /\
                          A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( vw vu vs vz va vt vy vc vr cme wcel wss wa ctotbnd cv cuni cbl cfv co
      wceq wrex wral cfn crp cdm wb cxp cres metres adantr syl5eqel eqid
      istotbnd2 syl w3a cvv simplr resexg dmexg 3syl ad2antrr simprr indexfi
      syl111anc wi cab firnfi2 adantl ad2antlr metssba2 dmeqi syl6reqr eqeq2d
      biimpa eqcomd adantlr visset eqeq1 rexbidv elab biimpri ralimi dfss3
      sylibr ad2antll uniss opreq1 rcla4ev mpan2 oprex cin simpr ssel2 sseq2d
      sylan adantllr blssp anassrs an1rs syldan eqtrd inss1 a1i eqsstrd sseq2
      syl11anc ex r19.23adva syl5ib r19.21aiv adantrr uniss2 sstrd adantlrr
      3adantr3 biimpd imdistani sstr2 impcom adantll ssrexv cbvrexv syl6ib
      3ad2antr1 unieq raleq anbi12d syl12anc mpd ralimdvaa sylbid c2 cdiv 2re
      2pos elrpii rpdivcl opreq2 ralbidv anbi2d rcla4v c0 wne crab ineq1
      neeq1d elrab inelcm sylan2 expcom jcad reximdv2 eluni2 3imtr4g ralimia
      3imtr4i ssrab2 ssralv ax-mp wex elin ancom bitri exbii n0 df-rex 3bitr4i
      biimpi sylbi rgen 3jca ssfi jctild 3anbi123d syl6 ssexg ancoms 3expb
      an1s a1d sseq1 cr cc0 clt wbr blssm rpregt0 syl2an eleq2d imbi12d mpbid
      imp syl5cbir unissb sylib elequ2 elequ1 syl5bb rcla4cva 3ad2antl2
      ad2ant2rl adantlll blhalf an4s sseld expimpd ancomsd eleq2 biimpcd
      reximdv id ad2ant2lr sylanr1 adantlrl simpll sylanbrc ancom2s 3ad2antl1
      adantrl eleqtrrd ssrdv eqssd eqtr4d biimpar jctird eqeq1d syld expdimp
      com23 exp3a 3impd r19.21adva sylibrd impbid ) CTUAZFEUBZUCZDUDUAZFBUEZUFZ
      UBZGUEZAUEZHUEZCUGUHZUIZUJZAEUKZGVUIULZUCZBUMUKZHUNULZVUGVUHKUEZUFZDUOZUO
      ZUJZLUEZMUEZVUNDUGUHZUIZUJZMVVFUKLVVCULZUCZKUMUKZHUNULZVVBVUGDTUAZVUHVVPU
      PVUGCFFUQZURZTDVUEVVSTUAVUFCFUSUTJVAZMKDVVFLHVVFVBZVCVDVUGVVOVVAHUNVUGVUN
      UNUAZUCZVVNVVAKUMVWCVVCUMUAZUCZVVNVVAVWEVVNUCZNUEZVVFUBZVVLMVWGUKZLVVCULZ
      VVLLVVCUKMVWGULZVEZNUMUKZVVAVWFVWDVVFVFUAZVVMVWMVWCVWDVVNVGVWCVWNVWDVVNVU
      EVWNVUFVWBVUEDVFUAVVEVFUAVWNVUEVVSVFDCVVRTVHJVADVFVIVVEVFVIVJVKVKVWEVVGVV
      MVLVVLLMVVCVVFVFNVMVNVWEVVGVWMVVAVOVVMVWEVVGUCVWLVVANUMVWEVVGVWGUMUAZVWLV
      VAVOVWEVVGVWOUCZUCZVWLVVAVWQVWLUCOUEZPUEZVUNVUOUIZUJZPVWGUKZOVPZUMUAZFVXC
      UFZUBZVURGVXCULZVVAVWPVXDVWEVWLVWOVXDVVGOPVWGVWTVQVRVSVWQVWHVWJVXFVWKVWEV
      VGVWHVWJUCZVXFVWOVWCVVGVXHVXFVWDVWCVVGUCZVXHUCFVVDVXEVXIFVVDUJZVXHVUGVVGV
      XJVWBVUGVVGUCVVDFVUGVVGVVDFUJVUGVVFFVVDVUGFVVSUOZUOVVFCEFIVTVVEVXKDVVSJWA
      WAWBZWCWDWEWFUTVWCVXHVVDVXEUBVVGVWCVXHUCZVVDVULVVKUJZMVWGUKZGVPZUFZVXEVXM
      VVCVXPUBZVVDVXQUBVWJVXRVWCVWHVWJVVHVXPUAZLVVCULVXRVWIVXSLVVCVXSVWIVXOVWIG
      VVHLWGVULVVHUJVXNVVLMVWGVULVVHVVKWHWIWJWKWLLVVCVXPWMWNWOVVCVXPWPVDVXMVUMQ
      UEZUBZQVXCUKZAVXPULZVXQVXEUBVWCVWHVYCVWJVWCVWHUCZVYBAVXPVYDVUMVVKUJZMVWGU
      KZVYBVUMVXPUAVYDVYEVYBMVWGVYDVVIVWGUAZUCZVYEVYBVYHVYEUCZVVIVUNVUOUIZVXCUA
      ZVUMVYJUBZVYBVYGVYKVYDVYEVYGVYJVWTUJZPVWGUKZVYKVYGVYJVYJUJZVYNVYJVBVYMVYO
      PVVIVWGVWSVVIUJZVWTVYJVYJVWSVVIVUNVUOWQWCWRWSVXBVYNOVYJVVIVUNVUOWTVWRVYJU
      JVXAVYMPVWGVWRVYJVWTWHWIWJWNVSVYIVUMVYJFXAZVYJVYIVUMVVKVYQVYHVYEXBVYHVVKV
      YQUJZVYEVYDVYGVVIFUAZVYRVUGVWHVYGVYSVWBVWGFUBZVYGVYSVUGVWHUCZVWGFVVIXCVUG
      VWHVYTVUGVVFFVWGVXLXDZWDXEXFVWCVYSVYRVWHVUGVYSVWBVYRVUGVYSVWBVYRVUNFCDEVV
      IIJXGXHXIWFXJUTXKVYQVYJUBVYIVYJFXLXMXNVYAVYLQVYJVXCVXTVYJVUMXOWRXPXQXRVXO
      VYFGVUMAWGZVULVUMUJVXNVYEMVWGVULVUMVVKWHWIWJXSXTYAAQVXPVXCYBVDYCWFXNXFYDY
      EVWQVWJVWHVXGVWKVWEVWHVXGVWPVWCVWHVXGVWDVUGVWHVXGVWBWUAVUGVYTUCVWGEUBZVXG
      VUGVWHVYTVUGVWHVYTWUBYFYGVUFVYTWUDVUEVYTVUFWUDVWGFEYHYIYJWUDVURGVXCWUDVUL
      VWTUJZPVWGUKZVURVULVXCUAWUDWUFWUEPEUKVURWUEPVWGEYKWUEVUQPAEVWSVUMUJVWTVUP
      VULVWSVUMVUNVUOWQWCYLYMVXBWUFOVULGWGVWRVULUJVXAWUEPVWGVWRVULVWTWHWIWJXSXT
      VJWFWFWFYNVUTVXFVXGUCBVXCUMVUIVXCUJZVUKVXFVUSVXGWUGVUJVXEFVUIVXCYOXDVURGV
      UIVXCYPYQWRYRXQXHXRYAYSXQXRYTUUAVUGVVBVVGVVIVUNRUEZVVJUIZUJZHVVFUKZMVVCUL
      ZUCZKUMUKZRUNULZVUHVUGVVBWUNRUNVUGWUHUNUAZUCZVVBVUKVULVUMWUHUUBUUCUIZVUOU
      IZUJZAEUKZGVUIULZUCZBUMUKZWUNWUPVVBWVDVOZVUGWUPWURUNUAZWVEWUPUUBUNUAWVFUU
      BUUDUUEUUFWUHUUBUUGWSVVAWVDHWURUNVUNWURUJZVUTWVCBUMWVGVUSWVBVUKWVGVURWVAG
      VUIWVGVUQWUTAEWVGVUPWUSVULVUNWURVUMVUOUUHWCWIUUIUUJWIUUKVDVRWUQWVCWUNBUMW
      UQVUIUMUAZUCZWVCFVVHUFZUBZWVAGVVHULZVXTVWSUAZQFUKZPVVHULZVEZLUMUKZWUNWVIW
      VCVWRFXAZUULUUMZOVUIUUNZUMUAZFWVTUFZUBZWVAGWVTULZWVNPWVTULZVEZUCWVQWVIWVC
      WWFWWAWVCWWFVOWVIWVCWWCWWDWWEVUKWWCWVBVXTVUJUAZQFULVXTWWBUAZQFULVUKWWCWWG
      WWHQFVXTFUAZVXTVUMUAZAVUIUKWWJAWVTUKWWGWWHWWIWWJWWJAVUIWVTWWIVUMVUIUAZWWJ
      UCZVUMWVTUAZWWJWWLWWIWWMWWKWWJWWIWWMWWKVUMFXAZUULUUMZWWMWWJWWIUCWWMWWKWWO
      UCWVSWWOOVUMVUIVWRVUMUJZWVRWWNUULVWRVUMFUUOUUPUUQWKVXTVUMFUURUUSXHUUTWWLW
      WJVOWWIWWKWWJXBXMUVAUVBAVXTVUIUVCAVXTWVTUVCUVDUVEQFVUJWMQFWWBWMUVFUTWVBWW
      DVUKWVTVUIUBZWVBWWDVOWVSOVUIUVGZWVAGWVTVUIUVHUVIVRWWEWVCWVNPWVTVWSWVTUAVW
      SVUIUAZVWSFXAZUULUUMZUCWVNWVSWXAOVWSVUIVWRVWSUJWVRWWTUULVWRVWSFUUOUUPUUQW
      XAWVNWWSWXAWVNVXTWWTUAZQUVJWWIWVMUCZQUVJWXAWVNWXBWXCQWXBWVMWWIUCWXCVXTVWS
      FUVKWVMWWIUVLUVMUVNQWWTUVOWVMQFUVPUVQUVRVRUVSUVTXMUWAXMWVHWWAWUQWVHWWQWWA
      WWRVUIWVTUWBWSVRUWCWVPWWFLWVTUMVVHWVTUJZWVKWWCWVLWWDWVOWWEWXDWVJWWBFVVHWV
      TYOXDWVAGVVHWVTYPWVNPVVHWVTYPUWDWRUWEWUQWVQWUNVOWVHWUQWVPWUNLUMWUQVVHUMUA
      ZUCZWVKWVLWVOWUNWXFWVKWVLWVOWUNVOWXFWVOWVKWVLUCZWUNWUQWXEWVOWXGWUNVOZWUQW
      XEWVOUCZVUIFUBZWVMQVUIUKZPVVHULZWVMPVVHUKQVUIULZVEZBUMUKZWXHVUGWXIWXOVOZW
      UPVUGFVFUAZWXPVUFVUEWXQVUFEVFUAWXQVUEFEVFUWFVUECUOZUOZVFEVUEWXRVFUAWXSVFU
      ACTVIWXRVFVIVDIVAUUSUWGWXQWXIWXOWXEWXQWVOWXOWXEWXQWVOWXOWVMPQVVHFVFBVMUWH
      UWIXQVDUTWUQWXNWXHBUMVUGWUPWVHWXNWXHVOVUGWUPWVHUCZUCZWXNWXHWYAWXNUCZWXGVW
      RVWGWUHVVJUIZUJZNVUIUKZOVPZUMUAZWYFUFZVVFUJZWUKMWYFULZUCZUCWUNWYBWXGWYGWY
      KWXTWXGWYGVOZVUGWXNWVHWYLWUPWVHWYGWXGONVUIWYCVQUWJVRVSWYBWXGWYIWYJWYBWXGW
      YIWYBWXGUCZWYHFVVFWYMWYHFWYBWYHFUBZWXGWYAWXLWXJWYNWXMVUGWUPWXJWYNWVHWUQWX
      JUCZVUMFUBZAWYFULWYNWYOWYPAWYFWYOVUMWYCUJZNVUIUKZWYPVUMWYFUAWYOWYQWYPNVUI
      WYQWYPWYCFUBZWYOVWGVUIUAZUCVUMWYCFUWKWUQWXJWYTWYSWUQVWGFUAZWYSWXJWYTUCWUQ
      XUAWYSWUQVWGVVFUAZWYCVVFUBZVOZXUAWYSVOZVVQWUHUWLUAUWMWUHUWNUWOUCZXUDVUGWU
      PVVQXUFUCXUBXUCVVQXUBXUFXUCDVWGWUHVVFVWAUWPXIXQVVTWUHUWQUWRVUGXUDXUEUPWUP
      VUGXUBXUAXUCWYSVUGVVFFVWGVXLUWSVUGVVFFWYCVXLXDUWTUTUXAUXBVUIFVWGXCUUSXHUX
      CXRWYEWYROVUMWUCWWPWYDWYQNVUIVWRVUMWYCWHWIWJXSXTAWYFFUXDWNYDYNUTWYMKFWYHW
      YMVVCFUAZVVCWYHUAZWYMXUGUCZVVCVUNUAZHWYFUKZXUHXUIVVCVVIUAZMVVHUKZXUKWXGXU
      GXUMWYBWVKXUGXUMWVLWVKXUGUCVVCWVJUAXUMFWVJVVCXCMVVCVVHUVCUXEWFYJXUIXULXUK
      MVVHWYMXUGVVIVVHUAZXULXUKVOWYMXUGXUNUCZUCZXULXUKXUPXULUCZSUEZVVIUAZSVUIUK
      ZXUKXUPXUTXULWXNWXGXUOXUTWYAWXNXUNXUTWXGXUGWXLWXJXUNXUTWXMWXKXUTPVVIVVHVY
      PXURVWSUAZSVUIUKXUTWXKVYPXVAXUSSVUIPMSUXFWIWVMXVAQSVUIQSPUXGYLUXHUXIUXJUX
      KUXLUTXUQXUSXUKSVUIXUPXULXURVUIUAZXUSXUKVOZWYMXUOXULXVBUCZXVCWYMXUOXVDUCZ
      UCZXUSXUKXVFXUSUCZXURWUHVVJUIZWYFUAZVVCXVHUAZXUKXVEXUSXVIWYMXVDXVIXUOXUSX
      VBXVIXULXVBXVHWYCUJZNVUIUKZXVIXVBXVHXVHUJZXVLXVHVBXVKXVMNXURVUIVWGXURUJWY
      CXVHXVHVWGXURWUHVVJWQWCWRWSWYEXVLOXVHXURWUHVVJWTVWRXVHUJWYDXVKNVUIVWRXVHW
      YCWHWIWJWNVRVSYJXVGVVCXURWUHVUOUIZFXAZXVHVVCXVOUAVVCXVNUAZXUGXVGVVCXVNFUV
      KWYMXVEXUSXVPWYAWXGXVEXUSUCZXVPWXNWYAWVLXVQXVPWVKWYAWVLUCXUNXULUCZXUSXVPX
      VEWYAWVLXVRXUSUCZXVPWYAVVCWUSUAZXURWUSUAZUCZAEUKZXVPWVLXVSUCZVUGWUPXWCXVP
      WVHWUQXWCXVPWUQXWBXVPAEWUQVUMEUAZUCXWAXVTXVPVUEWUPXWEXWAXVTUCXVPVOVUFVUEW
      UPUCZXWEUCXWAXVTXVPXWFXWEXWAXVTXVPVOXWFXWEXWAUCUCWUSXVNVVCVUEXWEWUPXWAWUS
      XVNUBWUHCEVUMXURIUXMUXNUXOXHUXPXFUXQXRUXBYDXWDVVIWUSUJZAEUKZXWCWVLXVRXWHX
      USWVLXUNXWHXULWVAXWHGVVIVVHVULVVIUJWUTXWGAEVULVVIWUSWHWIUXIYAYAXVSXWHXWCV
      OZWVLXULXUSXWIXUNXULXUSUCZXWGXWBAEXWGXWJXWBXWGXULXVTXUSXWAVVIWUSVVCUXRVVI
      WUSXURUXRYQUXSUXTYJVRYSUUSXHXUNXULXVRXUGXVBXVRUYAUYBUYCUYDXFXHXVEXUGWYMXU
      SXUGXUNXVDUYEVSUYFXVFXVHXVOUJZXUSWYMXVDXWKXUOWYBXVBXWKWXGXULWYAWXNXVBXWKW
      YAXURFUAZXWKWXNXVBUCVUGWUPXWLXWKWVHVUGWUPXWLXWKVUGXWLWUPXWKWUHFCDEXURIJXG
      UYGXHYDWXJWXLXVBXWLWXMVUIFXURXCUYHUUSXHUXKUYIUTUYJXUJXVJHXVHWYFVUNXVHVVCU
      XRWRXPXQXHXHXRYSXQXHXRYSHVVCWYFUVCWNXQUYKUYLWYAVVFFUJZWXNWXGVUGXWMWXTVXLU
      TVKUYMXQWYAWXLWXJWYJWXMVUGWXJWYJWXTVUGWXJUCZWUKMWYFXWNVVIWYCUJZNVUIUKZWUK
      VVIWYFUAXWNWUJHVUIUKZWUKXWPXWNVUIVVFUBZXWQWUKVOVUGXWRWXJVUGVVFFVUIVXLXDUY
      NWUJHVUIVVFYKVDXWOWUJNHVUIVWGVUNUJWYCWUIVVIVWGVUNWUHVVJWQWCYLXSWYEXWPOVVI
      MWGVWRVVIUJWYDXWONVUIVWRVVIWYCWHWIWJXSXTWFYNUYOUVAWUMWYKKWYFUMVVCWYFUJZVV
      GWYIWULWYJXWSVVDWYHVVFVVCWYFYOUYPWUKMVVCWYFYPYQWRUWEXQXHXRUYQUYRUYSUYTVUA
      XRUTUYQXRUYQVUBVUGVVQVUHWUOUPVVTHKDVVFMRVWAVCVDVUCVUD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M n p q r s t u v w x y z $.  $d X n p q r s t u v w x y z $.
    $d S n p q r s t u v w x y z $.
    totbndss.1 $e |- X = dom dom M $.
    $( A subset of a totally bounded metric space is totally bounded. $)
    totbndss $p |- ( ( M e. Met /\ M e. TotBnd /\ S C_ X ) ->
                                      ( M |` ( S X. S ) ) e. TotBnd ) $=
      ( vv vz vx vq vw vy vu vr vt vp vn vs cme wcel ctotbnd wss cxp cres wi
      wa cv cuni wceq cbl cfv co wrex wral cfn crp cdm c2 cdiv 2re 2pos elrpii
      rpdivcl mpan2 opreq2 eqeq2d rexbidv ralbidv anbi2d rcla4v syl adantl cpw
      cin c0 wne crab w3a cvv ssrab2 ssfi ad2antlr dmexg syl5eqel ssexg ex
      mpan9 pwexg ad2antrr ad2ant2r wex eleq2 inss1 sseli syl5cbi blhalf inss2
      oprex inex1 elpw mpbir a1i sseq1 biimparc ssrin 3ad2antl2 eqidd opreq1
      ineq1d rcla4ev syl11anc 3ad2ant3 adantr sseq2 eqeq1 anbi12d syl12anc
      3exp1 mpd an4s com23 imp exp3a anassrs mpdd r19.23adva an1rs 19.23adv
      rcla4cva adantll sylan2 n0 sylan2b anasss ineq1 neeq1d elrab r19.21aiva
      adantlr indexfi syl111anc uniss unipw syl6ss 3ad2ant1 ssel biimprd eluni
      syl6ib ad2antrl sseq1d anbi1d rcla4cv biimpri inelcm ancom13s syl5
      expdimp ssel2 elin sylan2br ancoms a1d expimpd reximdv eluni2 syl6ibr
      adantrr syld ssrdv adantllr 3adantr3 jcad eqss metssba2 sylibd eqid
      blssp ancom2s reximdva rexeqdv adantld ralimdvaa impr 3adantr2
      r19.21adva wb istotbnd2 metres 3imtr4d 3imp ) BQRZBSRZACTZBAAUAUBZSRZUWJU
      WLUWKUWNUWJUWLUWKUWNUCUWJUWLUDZEUEZUFZCUGZFUEZGUEZHUEZBUHUIZUJZUGZGCUKZFU
      WPULZUDZEUMUKZHUNULZIUEZUFZUWMUOUOZUGZJUEZKUEZLUEZUWMUHUIUJZUGZKUXLUKZJUX
      JULZUDZIUMUKZLUNULZUWKUWNUWOUXIUYBLUNUWOUXPUNRZUDZUXIUWRUWSUWTUXPUPUQUJZU
      XBUJZUGZGCUKZFUWPULZUDZEUMUKZUYBUYDUXIUYLUCZUWOUYDUYFUNRZUYMUYDUPUNRUYNUP
      URUSUTUXPUPVAVBUXHUYLHUYFUNUXAUYFUGZUXGUYKEUMUYOUXFUYJUWRUYOUXEUYIFUWPUYO
      UXDUYHGCUYOUXCUYGUWSUXAUYFUWTUXBVCVDVEVFVGVEVHVIVJUYEUYKUYBEUMUYEUWPUMRZU
      DZUYKUXJAVKZTZMUEZAVLZUXNTZUXNUXOUXPUXBUJZAVLZUGZKAUKZUDZJUXJUKZMNUEZAVLZ
      VMVNZNUWPVOZULZVUGMVULUKZJUXJULZVPZIUMUKZUYBUYQUYKVUQUYQUYKUDZVULUMRZUYRV
      QRZVUGJUYRUKZMVULULZVUQUYPVUSUYEUYKUYPVULUWPTVUSVUKNUWPVRUWPVULVSVBVTUYEU
      WRVUTUYPUYJUWOVUTUYDUWRUWOAVQRZVUTUWJCVQRZUWLVVCUWJBUOZUOZVQCUWJVVEVQRVVF
      VQRBQWAVVEVQWAVIDWBUWLVVDVVCACVQWCWDWEAVQWFVIWGWHUYEUYKVVBUYPUYEUYKUDZVVA
      MVULVVGUYTUWPRZVUAVMVNZUDVVAUYTVULRZVVGVVHVVIVVAVVGVVHUDZUXJVUARZIWIZVVAV
      VIVVKVVMVVAUYEUYKVVHVVMVVAUCZUYEUYTUYGUGZGCUKZVVNUYKVVHUDUYEVVPUDZVVLVVAI
      VVQVVLVVAUYEVVLVVPVVAUYEVVLUDZVVPVVAVVRVVOVVAGCVVRUWTCRZUDZVVOUXJUYGRZVVA
      VVLVVOVWAUCUYEVVSVVOUXJUYTRVWAVVLUYTUYGUXJWJVUAUYTUXJUYTAWKWLWMVTVVTVWAVV
      OVVAVVRVVSVWAVVOVVAUCZUCZUWOUYDVVLVVSVWCUCZUWJUYDUWLVVLVWDUWJUYDUDZUWLVVL
      UDZUDVVSVWAVWBVWEVWFVVSVWAUDZVWBUCVWEVWGVWFVWBVWEVWGVWFVWBUCZUWJVVSUYDVWA
      VWHUWJVVSUDUYDVWAUDZUDUYGUXJUXPUXBUJZTZVWHUXPBCUWTUXJDWNUWJVWKVWHUCVVSVWI
      UWJVWKVWFVVOVVAUWJVWKVWFVPZVVOUDZVWJAVLZUYRRZVUAVWNTZVWNVUDUGZKAUKZVVAVWO
      VWMVWOVWNATVWJAWOVWNAVWJAUXJUXPUXBWPWQWRWSWTVWKUWJVVOVWPVWFVWKVVOUDUYTVWJ
      TZVWPVVOVWSVWKUYTUYGVWJXAXBUYTVWJAXCVIXDVWLVWRVVOVWFUWJVWRVWKVVLVWRUWLVVL
      UXJARVWNVWNUGZVWRVUAAUXJUYTAWOWLVVLVWNXEVWQVWTKUXJAUXOUXJUGZVUDVWNVWNVXAV
      UCVWJAUXOUXJUXPUXBXFXGVDXHXIVJXJXKVUGVWPVWRUDJVWNUYRUXNVWNUGZVUBVWPVUFVWR
      UXNVWNVUAXLVXBVUEVWQKAUXNVWNVUDXMVEXNXHXOXPWGXQXRWDXSXTYAXRYBXTXSYCYDXTYE
      WDYFUYJVVHVVPUWRUYIVVPFUYTUWPUWSUYTUGUYHVVOGCUWSUYTUYGXMVEYGYHYIYBXTIVUAY
      JYKYLVUKVVINUYTUWPVUIUYTUGVUJVUAVMVUIUYTAYMYNYOYKYPYQVUGMJVULUYRVQIYRYSWD
      UYQUYKVUQUYBUCVURVUPUYAIUMVURUXJUMRZUDZVUPUXMUXTVXDVUPUXKAUGZUXMVXDVUPUXK
      ATZAUXKTZUDVXEVXDVUPVXFVXGVUPVXFUCVXDUYSVUMVXFVUOUYSUXKUYRUFAUXJUYRYTAUUA
      UUBUUCWTVXDVUPVXGVXDUYSVUMVXGVUOVURUYSVUMUDZVXGVXCUYEUYKVXHVXGUYPVVGVXHUD
      ZOAUXKVXIOUEZARZVXJCRZVXJUXKRZUYEVXKVXLUCZUYKVXHUWLVXNUWJUYDACVXJUUDVTWGV
      XIVXKVXLVXMUCVXIVXKUDZVXLVXJPUEZRZVXPUWPRZUDZPWIZVXMVVGVXLVXTUCZVXHVXKUWR
      VYAUYEUYJUWRVXLVXJUWQRZVXTUWRVYBVXLUWQCVXJWJUUEPVXJUWPUUFUUGUUHWGVXOVXSVX
      MPVXOVXSVXPAVLZUXNTZVUFUDZJUXJUKZVXMVXHVXKVXSVYFUCZVVGVUMVXKVYGUYSVUMVXKV
      XSVYFVUMVXPVULRZVYFVXKVXSUDVUHVYFMVXPVULUYTVXPUGZVUGVYEJUXJVYIVUBVYDVUFVY
      IVUAVYCUXNUYTVXPAYMUUIUUJVEUUKVXRVXQVXKVYHVXRVYCVMVNZVYHVXQVXKUDZVYHVXRVY
      JUDVUKVYJNVXPUWPVUIVXPUGVUJVYCVMVUIVXPAYMYNYOUULVXJVXPAUUMYIUUNUUOUUPYHYH
      VXKVXSVYFVXMUCZUCVXIVXKVXSVYLVXKVXQVYLVXRVXQVXKVYLVYKVYFVXJUXNRZJUXJUKVXM
      VYKVYEVYMJUXJVYKVYDVUFVYMVYKVYDUDVYMVUFVYDVYKVYMVYDVXJVYCRVYMVYKVYCUXNVXJ
      UUQVXJVXPAUURUUSUUTUVAUVBUVCJVXJUXJUVDUVEUUTUVFWDVJYCYFUVGWDYCUVHUVIYQUVJ
      WDUVKUXKAUVLUVEVXDAUXLUXKUYQAUXLUGZUYKVXCUWOVYNUYDUYPBCADUVMZWGWGVDUVNUYQ
      VUPUXTUCZUYKVXCUYEVYPUYPUYEVUPUXTUYEUYSVUOUXTVUMUYEUYSVUOUXTUYEUYSUDZVUNU
      XSJUXJVYQUXNUXJRZUDZVUGUXSMVULVYSVUGUXSUCVVJVYSVUFUXSVUBVYSVUFUXRKAUKUXSV
      YSVUEUXRKAVYSUXOARZUDZUXRVUEWUAUXQVUDUXNVYQVYTUXQVUDUGZVYRUYEVYTWUBUYSUWO
      UYDVYTWUBUWOVYTUYDWUBUXPABUWMCUXODUWMUVOUVPUVQYBYQYQVDUUEUVRVYSUXRKAUXLUY
      EVYNUYSVYRUWOVYNUYDVYOXKWGUVSUVNUVTXKYDUWAUWBUWCWDXKWGUVKUVRWDYCYDUVGUWDU
      WJUWKUXIUWEUWLGEBCFHDUWFXKUWJUWNUYCUWEZUWLUWJUWMQRWUCBAUWGKIUWMUXLJLUXLUV
      OUWFVIXKUWHWDXSUWI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d m x r $.
    $( Define the class of bounded metrics.  A metric space is bounded iff it
       can be covered by a single ball. $)
    df-bnd $a |- Bnd = { m e. Met | A. x e. dom dom m E. r e. RR+
                                          dom dom m = ( x ( ball ` m ) r ) } $.
  $}

  ${
    $d M m x r $.  $d X m x r $.
    isbnd.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space". $)
    isbnd $p |- ( M e. Bnd <-> ( M e. Met /\ A. x e. X E. r e. RR+
                            X = ( x ( ball ` M ) r ) ) ) $=
      ( vm cv cdm cbl cfv co wceq crp wrex wral cme cbnd dmeq dmeqd syl6eqr
      fveq2 opreqd eqeq12d rexbidv raleqbidv df-bnd elrab2 ) FGZHZHZAGZDGZUHIJZ
      KZLZDMNZAUJOCUKULBIJZKZLZDMNZACOFBPQUHBLZUPUTAUJCVAUJBHZHCVAUIVBUHBRSETZV
      AUOUSDMVAUJCUNURVCVAUMUQUKULUHBIUAUBUCUDUEAFDUFUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M m x r $.  $d X m x r $.
    isbnd2.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space". $)
    isbnd2 $p |- ( M e. Met -> ( M e. Bnd <->
                        A. x e. X E. r e. RR+ X = ( x ( ball ` M ) r ) ) ) $=
      ( cbnd wcel cme cv cbl cfv co wceq crp wrex wral isbnd baib ) BFGBHGCAIDI
      BJKLMDNOACPABCDEQR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x y r s $.  $d X x y r s $.
    isbnd3.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  Uses a single point instead
       of an arbitrary point in the space. $)
    isbnd3 $p |- ( ( M e. Bnd /\ X =/= (/) ) <-> ( M e. Met /\
                E. x e. X E. r e. RR+ X = ( x ( ball ` M ) r ) ) ) $=
      ( vy vs cbnd wcel c0 wne wa cme cv cbl cfv co wceq crp wrex wral isbnd
      anbi1i anass r19.2z ancoms wi c2 cmul 2re 2pos elrpii rpmulcl mpan
      ad2antll ad2antrr cdiv wss cc cc0 rpcn 2cn a1i 2ne0 w3a divcan3 eqcomd
      syl111anc opreq2d eqeq2d biimpd adantr imp simpr blhalf expr sylan2
      anasss eleq2 biimpac anassrs eqsstrd syldan cr clt wbr blssm elrp sylib
      an1rs adantlrl eqssd opreq2 rcla4ev syl11anc ex r19.21adva r19.23advv
      opreq1 rexbidv cbvrexv rexbii bitri syl5ib rexn0 jcad impbid2 pm5.32i
      3bitri ) BHIZCJKZLBMIZCANZDNZBOPZQZRZDSTZACUAZLZXKLXLXSXKLZLXLXRACTZLXJXT
      XKABCDEUBUCXLXSXKUDXLYAYBXLYAYBXKXSYBXRACUEUFXLYBXSXKXLCFNZGNZXOQZRZGSTZF
      CTZXSYBXLYFXSFGCSXLYCCIZYDSIZLZYFXSUGXLYKLZYFXRACYLXMCIZLZYFXRYNYFLZUHYDU
      IQZSIZCXMYPXOQZRZXRYLYQYMYFYJYQXLYIUHSIYJYQUHUJUKULUHYDUMUNZUOUPYOCYRYNYF
      CYCYPUHUQQZXOQZRZCYRURYNYFUUCYLYFUUCUGZYMYJUUDXLYIYJYFUUCYJYEUUBCYJYDUUAY
      CXOYJYDUSIZUHUSIZUHUTKZYDUUARYDVAUUFYJVBVCUUGYJVDVCUUEUUFUUGVEUUAYDYDUHVF
      VGVHVIVJVKUOVLVMYNUUCLCUUBYRYNUUCVNYLYMUUCUUBYRURZYLXMUUBIZUUHYMUUCLYLUUI
      UUHXLYIYJUUIUUHUGZXLYILZYQUUJYJUUKYQUUIUUHYPBCYCXMEVOVPYTVQVRVMUUCYMUUICU
      UBXMVSVTVQWAWBWCYNYRCURZYFXLYJYMUULYIXLYMYJUULXLYMLYPWDIUTYPWEWFLZUULYJBX
      MYPCEWGYJYQUUMYTYPWHWIVQWJWKVLWLXQYSDYPSXNYPRXPYRCXNYPXMXOWMVJWNWOWPWQWPW
      RYBCYCXNXOQZRZDSTZFCTYHXRUUPAFCXMYCRZXQUUODSUUQXPUUNCXMYCXNXOWSVJWTXAUUPY
      GFCUUOYFDGSXNYDRUUNYECXNYDYCXOWMVJXAXBXCXDYBXKUGXLXRACXEVCXFXGXHXI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x y r $.  $d X x y r $.  $d S x y r $.
    bndss.1 $e |- X = dom dom M $.
    $( A subset of a bounded metric space is bounded. $)
    bndss $p |- ( ( M e. Bnd /\ S C_ X ) -> ( M |` ( S X. S ) ) e. Bnd ) $=
      ( vy vr vx cme wcel cv cbl cfv co wceq crp wrex wral wa wss cxp cres cdm
      cbnd metres ad2antrr metssba2 eleq2d biimpar adantllr wi opreq1 eqeq2d
      rexbidv rcla4va ssel2 ancoms sylan adantlll cin eqid blssp an4s anassrs
      adantr dfss biimpi incom syl6eq ineq1 sylan9eq adantll adantlr 3eqtr2rd
      ex reximdva imp syldan an1rs r19.21aiva jca isbnd anbi1i 3imtr4i ) BHIZCE
      JZFJZBKLZMZNZFOPZECQZRZACSZRZBAATUAZHIZWOUBUBZGJZWFWOKLMZNZFOPZGWQQZRBUCI
      ZWMRWOUCIWNWPXBWDWPWKWMBAUDUEWNXAGWQWNWRWQIZWRAIZXAWDWMXDXEWKWDWMRZXEXDXF
      AWQWRBCADUFZUGUHUIWLXEWMXAWLXERWMXAWDXEWKWMXAUJWDXERZWKRWMXAXHWMWKXAXHWMR
      ZWKCWRWFWGMZNZFOPZXAXEWMWKXLWDWRCIZWKXLXEWMRWJXLEWRCWEWRNZWIXKFOXNWHXJCWE
      WRWFWGUKULUMUNWMXEXMACWRUOUPUQURXIXLXAXIXKWTFOXIWFOIZRZXKWTXPXKRWSXJAUSZA
      WQXPWSXQNZXKXHWMXOXRWDWMXEXOXRWFABWOCWRDWOUTVAVBVCVDXIXKAXQNZXOWMXKXSXHWM
      XKACAUSZXQWMAACUSZXTWMAYANACVEVFACVGVHCXJAVIVJVKVLXIAWQNZXOXKWDWMYBXEXGVL
      UEVMVNVOVPVQVRVNVRVPVRVQVSVTXCWLWMEBCFDWAWBGWOWQFWQUTWAWC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x r $.  $d X x r $.  $d Y x r $.  $d R x r $.
    blbnd.1 $e |- X = dom dom M $.
    $( A ball is bounded. $)
    blbnd $p |- ( ( M e. Met /\ Y e. X /\ R e. RR+ ) ->
          ( M |` ( ( Y ( ball ` M ) R ) X. ( Y ( ball ` M ) R ) ) ) e. Bnd ) $=
      ( vx vr cme wcel crp w3a cbl cfv co cxp cres cbnd cdm c0 wne wa cv wceq
      wrex eqid isbnd3 metres 3ad2ant1 cr cc0 clt wbr blcntr elrp sylan2b
      3impa wss simp1 blssm metssba2 syl11anc eleqtrd simp3 cin blssp syl22anc
      inidm syl6eq eqtr2d opreq2 eqeq2d rcla4ev opreq1 rexbidv sylanbrc simpld
      ) BHIZDCIZAJIZKZBDABLMNZWAOPZQIZWBRRZSTZWCWEUAWBHIZWDFUBZGUBZWBLMZNZUCZGJ
      UDZFWDUDZVTFWBWDGWDUEUFVQVRWFVSBWAUGUHVTDWDIWDDWHWINZUCZGJUDZWMVTDWAWDVQV
      RVSDWAIZVQVRUAZAUIIUJAUKULUAZWQVSBDACEUMAUNZUOUPZVTVQWACUQZWAWDUCVQVRVSUR
      ZVQVRVSXBWRWSXBVSBDACEUSWTUOUPZBCWAEUTVAZVBVTVSWDDAWINZUCZWPVQVRVSVCZVTXF
      WAWDVTXFWAWAVDZWAVTVQXBWQVSXFXIUCXCXDXAXHAWABWBCDEWBUEVEVFWAVGVHXEVIWOXGG
      AJWHAUCWNXFWDWHADWIVJVKVLVAWLWPFDWDWGDUCZWKWOGJXJWJWNWDWGDWHWIVMVKVNVLVAV
      OVP $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M a b c d m r s t u v x y z $.  $d X a b c d m r s t u v x y z $.
    totbndbnd.1 $e |- X = dom dom M $.
    $( A totally bounded metric space is bounded. $)
    totbndbnd $p |- ( M e. TotBnd -> M e. Bnd ) $=
      ( vv vb vx vd vy vr vu vs va vc vz vt cme wcel ctotbnd cbnd totbndmet cv
      cuni wceq cbl cfv co wrex wral wa cfn crp c1 wss w3a cvv simplr cdm
      dmexg syl syl5eqel adantr ad2antrr simprr indexfi syl111anc wne clt wbr
      wi caddc crab c0 cdom copab crn cab eleq1 cr cc0 elrp readdcl metcl 1re
      sylancl cle metge0 lt01 jctir 0re a1i leltadd syl22anc mpd 0cn addid1i
      syl5eqbrr sylanbrc 3expa syl5cbir ssel2 sylan2 anassrs r19.23adva
      pm4.71rd abbidv adantlr 3ad2antr1 rnopab2 df-rab 3eqtr4g wfn visset
      oprex eqid fnopab2 fnrndomg mp2 syl6eqbrr domfi syl11anc wn dmopab2
      eqeq1i dm0rn0 bitr3i rex0 rexeq mtbiri r19.2z eqidd reximi r19.23aiv
      ne0i eqeq1 unieq uni0 syl6eq syl5bi necon3d mpan9 sylan nsyl sylbir
      necon2ai adantlll 3ad2antr2 eqnetrrd wor ssrab2 rpssre sstri ltso soss
      fimax eleq2 eluni2 biimpi syl6bir ad2antlr rexbidv rcla4cva 3ad2antl2
      adantll ax-17 hbre1 hbral hbra1 hb3an hban breq1 rcla4va elrab peano2re
      opreq2 opreq1d eqeq2d rcla4ev mpan2 adantl anasss ancom2s rpre eqle ex
      equcom syl5ib idd necon3bd ltle syl2an ancoms imim12d pm2.61 sylsyld
      simplbi ralimdvaa imp anim1i wb adantllr ltadd2 biimpd ltletr mettri
      3exp2 imp41 an1rs lelttr mpand syld exp3a com23 3ad2antl1 anim2i
      imdistand elbl mpanr12 sylan2b 3imtr4d biimpa syl5 exp4b r19.23ad blssm
      sseld impbid eqrdv reximdva ssrexv ax-mp adantlrr mpdan 1rp ralbidv
      anbi2d rcla4v r19.21adva istotbnd2 isbnd2 mpcom ) APQZARQZASQZATVUADUAZUB
      ZBUCZEUAZFUAZGUAZAUDUEZUFZUCZFBUGZEVUDUHZUIZDUJUGZGUKUHZBHUAZIUAZVUJUFZUC
      ZIUKUGZHBUHVUBVUCVUAVUQVVBHBVUAVURBQZUIZVUFVUGVUHULVUJUFZUCZFBUGZEVUDUHZU
      IZDUJUGZVVBVUQVVDVVIVVBDUJVVDVUDUJQZUIZVVIVVBVVLVVIUIZJUAZBUMZVVFFVVNUGZE
      VUDUHZVVFEVUDUGZFVVNUHZUNZJUJUGZVVBVVMVVKBUOQZVVHVWAVVDVVKVVIUPVVDVWBVVKV
      VIVUAVWBVVCVUAAUQZUQZUOBVUAVWCUOQVWDUOQAPURVWCUOURUSCUTVAVBVVLVUFVVHVCVVF
      EFVUDBUOJVDVEVVLVUFVWAVVBVVHVVDVUFVWAVVBVVKVVDVUFUIZVWAVVBVWEVVTVVBJUJVWE
      VVNUJQZUIZVVTVVBVWGVVTUIZVUSKUAZVFZVWIVUSVGVHZVIZKLUAZVURMUAZAUFZULVJUFZU
      CZMVVNUGZLUKVKZUHZIVWSUGZVVBVWHVWSUJQZVWSVLVFVXAVWHVWFVWSVVNVMVHVXBVWEVWF
      VVTUPVWHVWSVWNVVNQZVWQUIMLVNZVOZVVNVMVWHVWRLVPZVWMUKQZVWRUIZLVPZVXEVWSVWG
      VVQVVOVXFVXIUCZVVSVWEVVOVXJVWFVVDVVOVXJVUFVVDVVOUIZVWRVXHLVXKVWRVXGVXKVWQ
      VXGMVVNVVDVVOVXCVWQVXGVIZVVDVWNBQZVXLVVOVXCUIVWQVXGVWPUKQZVVDVXMUIVWMVWPU
      KVQVUAVVCVXMVXNVXNVWPVRQZVSVWPVGVHVUAVVCVXMUNZVWPVTVWOVRQZULVRQZVXOVXPVWO
      ULWAVURVWNABCWBZWCWDVXPVSVSVJUFZVWPVSVGVXPVSVWOWEVHZVSULVGVHZUIZVXTVWPVGV
      HZVXPVYAVYBVURVWNABCWFWGWHVXPVSVRQZVYEVXQVXRVYCVYDVIVYEVXPWIWJZVYFVXSVXRV
      XPWCWJVSVSVWOULWKWLWMVSWNWOZWPWQWRWSVVNBVWNWTXAXBXCXDXEXFXFXGMLVVNVWPXHVW
      RLUKXIXJZVVNUOQVXDVVNXKVXEVVNVMVHJXLMLVVNVWPVXDVWOULVJXMZVXDXNZXOVVNUOVXD
      XPXQXRVVNVWSXSXTVWHVXEVWSVLVYHVWGVVOVVQVXEVLVFZVVSVWEVVQVYKVWFVVCVUFVVQVY
      KVUAVVCVUFUIZVVQUIZVXEVLVXEVLUCZVVNVLUCZVYMYAVYOVXDUQZVLUCVYNVYPVVNVLMLVV
      NVWPVXDVYIVYJYBYCVXDYDYEVYOVUHVUHUCZFVVNUGZVYMVYOVYRVYQFVLUGVYQFYFVYQFVVN
      VLYGYHVUDVLVFZVVQVYRVYLVYSVVQUIVVPEVUDUGVYRVVPEVUDYIVVPVYREVUDVVPVYRVIVUG
      VUDQZVVFVYQFVVNVVFVUHYJYKWJYLUSVVCBVLVFVUFVYSBVURYMVUFVUDVLBVLVUFVUEVLUCB
      VLUCVUDVLUCZVUEBVLYNWUAVUEVLUBVLVUDVLYOYPYQYRYSYTUUAUUBUUCUUDUUEXFUUFUUGI
      KVWSVGVWSVRUMVRVGUUHVWSVGUUHVWSUKVRVWRLUKUUIZUUJUUKUULVWSVRVGUUMXQUUNXTVW
      HVWTIUKUGZVVBVXAVWHVWTVVAIUKVWEVVTVUSUKQZVWTVVAVIVWFVWEVVTUIZWUDUIZVWTVVA
      WUFVWTUIZNBVUTWUGNUAZBQZWUHVUTQZWUGWUIWUHOUAZQZOVUDUGZWUJWUEWUIWUMVIZWUDV
      WTVUFWUNVVDVVTVUFWUIWUHVUEQZWUMVUEBWUHUUOWUOWUMOWUHVUDUUPUUQUURUUSVBWUGWU
      LWUJOVUDWUGWUKVUDQZUIZWUKVVEUCZFVVNUGZWULWUJVIZWUFWUPWUSVWTWUEWUPWUSWUDVV
      TWUPWUSVWEVVQVVOWUPWUSVVSVVPWUSEWUKVUDVUGWUKUCVVFWURFVVNVUGWUKVVEYNUUTUVA
      UVBUVCXFXFWUQWURWUTFVVNWUGWUPFWUFVWTFWUEWUDFVWEVVTFVWEFUVDVVOVVQVVSFVVOFU
      VDVVPFEVUDVYTFUVDVVFFVVNUVEUVFVVRFVVNUVGUVHUVIWUDFUVDUVIVWTFUVDUVIWUPFUVD
      UVIWUTFUVDWUQVUHVVNQZWURWULWUJWUQWVAUIZWUHVVEQZWUJWURWULUIWVBWUIVUHWUHAUF
      ZULVGVHZUIZWUIVURWUHAUFZVUSVGVHZUIZWVCWUJWVBWUIWVEWVHWUGWVAWUIWVEWVHVIZVI
      ZWUPWUGWVAUIVURVUHAUFZULVJUFZVUSWEVHZWVKWUFVWTWVAWVNWUEWUDVWTWVAUIZWVNWUE
      WUDWVOUIZWVNVWEVVQVVOWVPWVNVIZVVSVVDVVOWVQVUFVXKWVPWVNVXKVWIVUSWEVHZKVWSU
      HZWVAUIZWVNWVPVXKWVAWVSWVNVXKWVAWVSWVNWVMVWSQZWVSWVNVXKWVAUIZWVRWVNKWVMVW
      SVWIWVMVUSWEUVJUVKWWAWVMUKQZWVMVWPUCZMVVNUGZWWBVWRWWELWVMUKVWMWVMUCVWQWWD
      MVVNVWMWVMVWPYNUUTUVLVVDVVOWVAWWCVVDVUHBQZWWCVVOWVAUIVUAVVCWWFWWCWWCWVMVR
      QZVSWVMVGVHVUAVVCWWFUNZWVMVTWWHWVLVRQZWWGVURVUHABCWBZWVLUVMUSWWHVXTWVMVSV
      GWWHVSWVLWEVHZVYBUIZVXTWVMVGVHZWWHWWKVYBVURVUHABCWFWGWHWWHVYEVYEWWIVXRWWL
      WWMVIVYEWWHWIWJZWWNWWJVXRWWHWCWJVSVSWVLULWKWLWMVYGWPWQWRVVNBVUHWTZXAXBWVA
      WWEVXKWVAWVMWVMUCZWWEWVMXNWWDWWPMVUHVVNVWNVUHUCZVWPWVMWVMWWQVWOWVLULVJVWN
      VUHVURAUVNUVOUVPUVQUVRUVSWQUUAUVTUWAWUDVWTWVAWVTWUDVWTUIWVSWVAWUDVWTWVSWU
      DVWLWVRKVWSWUDVWIUKQZVWLWVRVIVWIVWSQZWUDWWRUIZVUSVWIUCZWVRVIZVWLWXAYAZWVR
      VIWVRWWRWXBWUDWWRVWIVRQZWXBVWIUWBZWXDVWIVUSUCZWVRWXAWXDWXFWVRVWIVUSUWCUWD
      IKUWEUWFUSUVSWWTWXCVWJVWKWVRWWTWXAVUSVWIWWTWXAUWGUWHWWRWUDVWKWVRVIZWXDVUS
      VRQZWXGWWRWUDVWIVUSUWIWXEVUSUWBZUWJUWKUWLWXAWVRUWMUWNWWSWWRVWIVWPUCZMVVNU
      GZVWRWXKLVWIUKVWMVWIUCVWQWXJMVVNVWMVWIVWPYNUUTUVLUWOXAUWPUWQUWRUVTXAUWDXF
      XGUWQXBXBWUFWVAWVNWVKVIZVWTWUEWVAWUDWXLVWEWWFUIZWUDWXLWUEWVAUIVVDWWFWUDWX
      LVUFVVDWWFUIZWUDUIZWUIWVNWVJWXOWUIWVNWVJVIWXOWUIUIZWVEWVNWVHWXPWVEWVLWVDV
      JUFZWVMVGVHZWVNWVHVIWXNWUIWVEWXRVIWUDWXNWUIUIZWVEWXRWXSWVDVRQZVXRWWIWVEWX
      RUWSVUAWWFWUIWXTVVCVUAWWFWUIWXTVUHWUHABCWBWRUWTZVXRWXSWCWJWXNWWIWUIVUAVVC
      WWFWWIWWJWRZVAZWVDULWVLUXAVEUXBXFWXPWXRWVNWVHWXPWXRWVNUIZWXQVUSVGVHZWVHWX
      PWXQVRQZWWGWXHWYDWYEVIWXPWWIWXTWYFWXNWWIWUDWUIWYBVBWXNWUIWXTWUDWYAXFWVLWV
      DWAXTZWXNWUIWWGWUDWWIVXRWWGWXSWVLULWAWYCWCWDXFWUDWXHWXNWUIWXIUUSZWXQWVMVU
      SUXCVEWXPWVGWXQWEVHZWYEWVHWXNWUIWYIWUDVVDWUIWWFWYIVUAVVCWUIWWFWYIVUAVVCWU
      IWWFWYIVURWUHVUHABCUXDUXEUXFUXGXFWXPWVGVRQZWYFWXHWYIWYEUIWVHVIWXNWUIWYJWU
      DVVDWUIWYJWWFVUAVVCWUIWYJVURWUHABCWBWRXFXFWYGWYHWVGWXQVUSUXHVEUXIUXJUXKUX
      JUXLUWDUXLUWTVWEVVTWVAWXMVVTWVAUIZWWFVWEVVOVVQWVAWWFVVSWWOUXMZUXNXBUUAUXG
      XFWMXFUXOWUGWVAWVCWVFUWSZWUPWUFWVAWYMVWTWUEWVAWYMWUDVWEVVTWVAWYMVWEWWFWYM
      WYKVVDWWFWYMVUFVUAWWFWYMVVCVUAWWFUIVXRVYBWYMWCWGWUHAVUHULBCUXPUXQXFXFWYLX
      AXBXFXFXFWUGWUJWVIUWSZWUPWVAWUFWYNVWTVWEWUDWYNVVTVVDWUDWYNVUFVVDWXHVSVUSV
      GVHUIZWYNWUDWUHAVURVUSBCUXPVUSVTZUXRXFXFVAVBUXSWURWULWVCWUKVVEWUHUUOUXTUY
      AUYBUYCWMXCUXJWUFWUJWUIVIZVWTVWEWUDWYQVVTVVDWUDWYQVUFVVDWUDUIVUTBWUHVVDWY
      OVUTBUMWUDAVURVUSBCUYDWYPUXRUYEXFXFVAUYFUYGUWDUWTUYHVWSUKUMVXAWUCVIWUBVWT
      IVWSUKUYIUYJUYAWMUWDXCUWQUWTUYKUYLUWDXCULUKQVUQVVJVIUYMVUPVVJGULUKVUIULUC
      ZVUOVVIDUJWYRVUNVVHVUFWYRVUMVVGEVUDWYRVULVVFFBWYRVUKVVEVUGVUIULVUHVUJUVNU
      VPUUTUYNUYOUUTUYPUYJUYAUYQFDABEGCUYRHABICUYSUXSUYT $.
      $( [2-Sep-2009] $)
  $}

