$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Commutative rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CRing $.

  $( Extend class notation with the class of commutative rings. $)
  ccring $a class CRing $.

  $( Define the class of commutative rings. $)
  df-cring $a |- CRing = ( RingOps i^i Com2 ) $.

  $( The predicate "is a commutative ring".  (Contributed by Jeff Madsen,
     8-Jun-2010.) $)
  iscring $p |- ( R e. CRing <-> ( R e. RingOps /\ R e. Com2 ) ) $=
    ( ccring wcel cring ccm2 cin wa df-cring eleq2i elin bitri ) ABCADEFZCADCAE
    CGBLAHIADEJK $.
    $( [8-Jun-2010] $)

  ${
    $d R x y $.  $d X x y $.
    iscring2.1 $e |- G = ( 1st ` R ) $.
    iscring2.2 $e |- H = ( 2nd ` R ) $.
    iscring2.3 $e |- X = ran G $.
    $( The predicate "is a commutative ring".  (Contributed by Jeff Madsen,
       8-Jun-2010.) $)
    iscring2 $p |- ( R e. CRing <-> ( R e. RingOps /\
                          A. x e. X A. y e. X ( x H y ) = ( y H x ) ) ) $=
      ( wcel cring ccm2 wa cv co wceq wral c1st cfv cvv ccring iscring c2nd cop
      wb wrel relrng 1st2nd mpan eleq1 rneqi eqtri raleqi opreqi eqeq12i ralbii
      crn bitri fvex iscom2 mp2an 3bitr4ri syl6bb syl pm5.32i ) CUAJCKJZCLJZMVF
      ANZBNZEOZVIVHEOZPZBFQZAFQZMCUBVFVGVNVFCCRSZCUCSZUDZPZVGVNUEKUFVFVRUGCKUHU
      IVRVGVQLJZVNCVQLUJVHVIVPOZVIVHVPOZPZBVOUQZQZAFQWDAWCQZVNVSWDAFWCFDUQWCIDV
      OGUKULZUMVMWDAFVMWBBFQWDVLWBBFVJVTVKWAEVPVHVIHUNEVPVIVHHUNUOUPWBBFWCWFUMU
      RUPVOTJVPTJVSWEUECRUSCUCUSTTVOVPABUTVAVBVCVDVEUR $.
      $( [8-Jun-2010] $)
  $}

  ${
    $d ph w x y z $.  $d G w x y z $.  $d H w x y z $.  $d X w x y z $.
    $d U x y $.
    iscringd.1 $e |- ( ph -> G e. AbelOp ) $.
    iscringd.2 $e |- ( ph -> X = ran G ) $.
    iscringd.3 $e |- ( ph -> H : ( X X. X ) --> X ) $.
    iscringd.4 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                                  -> ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $.
    iscringd.5 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $.
    iscringd.6 $e |- ( ph -> U e. X ) $.
    iscringd.7 $e |- ( ( ph /\ y e. X ) -> ( y H U ) = y ) $.
    iscringd.8 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                                              -> ( x H y ) = ( y H x ) ) $.
    $( Conditions that determine a commutative ring.  (Contributed by Jeff
       Madsen, 20-Jun-2011.)  (Revised by Mario Carneiro, 23-Dec-2013.) $)
    iscringd $p |- ( ph -> <. G , H >. e. CRing ) $=
      ( wcel co wceq wa vw cop cring ccm2 ccring cv w3a id 3com13 eleq1 3anbi1d
      wi anbi2d opreq2 opreq12d eqeq12d imbi12d 3anbi3d opreq1 opreq1d cabl crn
      adantr simpr3 eleqtrd simpr2 ablocom syl111anc simpr1 cgr ablogrpo grpocl
      eqid syl eleqtrrd jca oprex chvarv syldan 3simpa sylan2 3simpb cxp foprrn
      vtocl wf 3eqtrd 3eqtr2d imbi2d an12s ex vtoclga mpcom eqtrd isringod wral
      eleq2d anbi12d biimpar r19.21aivva wb rnexg eqeltrd xpexg syl11anc iscom2
      cvv fex mpbird iscring sylanbrc ) AFGUBZUCQXLUDQZXLUEQABCDEFGHIJKLMBUFZHQ
      ZCUFZHQZDUFZHQZUGZAXSXQXOUGZXNXPFRZXRGRZXNXRGRZXPXRGRZFRZSZXSXQXOYAYAUHUI
      AUAUFZHQZXQXOUGZTZYBYHGRZXNYHGRZXPYHGRZFRZSZULZAYATZYGULUADYHXRSZYKYRYPYG
      YSYJYAAYSYIXSXQXOYHXRHUJUKUMYSYLYCYOYFYHXRYBGUNYSYMYDYNYEFYHXRXNGUNYHXRXP
      GUNUOUPUQAYIXQXSUGZTZXRXPFRZYHGRZXRYHGRZYNFRZSZULZYQDBXRXNSZUUAYKUUFYPUUH
      YTYJAUUHXSXOYIXQXRXNHUJURUMUUHUUCYLUUEYOUUHUUBYBYHGXRXNXPFUSUTUUHUUDYMYNF
      XRXNYHGUSUTUPUQAXTTZUUBXNGRZXRXNGRZXPXNGRZFRZSZULUUGBUAXNYHSZUUIUUAUUNUUF
      UUOXTYTAUUOXOYIXQXSXNYHHUJUKUMUUOUUJUUCUUMUUEXNYHUUBGUNUUOUUKUUDUULYNFXNY
      HXRGUNXNYHXPGUNUOUPUQUUIUUJXPXRFRZXNGRZXNUUPGRZUUMUUIUUBUUPXNGUUIFVAQZXRF
      VBZQZXPUUTQZUUBUUPSAUUSXTIVCZUUIXRHUUTAXOXQXSVDZAHUUTSXTJVCZVEZUUIXPHUUTA
      XOXQXSVFZUVEVEZXRXPFUUTUUTVMZVGVHUTAXTXOUUPHQZTZUURUUQSZUUIXOUVJAXOXQXSVI
      ZUUIUUPUUTHUUIFVJQZUVBUVAUUPUUTQUUIUUSUVNUVCFVKVNUVHUVFXPXRFUUTUVIVLVHUVE
      VOVPAXOYITZTZYMYHXNGRZSZULZAUVKTZUVLULUAUUPXPXRFVQYHUUPSZUVPUVTUVRUVLUWAU
      VOUVKAUWAYIUVJXOYHUUPHUJUMUMUWAYMUURUVQUUQYHUUPXNGUNYHUUPXNGUSUPUQAXOXQTZ
      TZXNXPGRZUULSZULZUVSCUAXPYHSZUWCUVPUWEUVRUWGUWBUVOAUWGXQYIXOXPYHHUJUMUMUW
      GUWDYMUULUVQXPYHXNGUNXPYHXNGUSUPUQPVRWEVSUUIUURUWDYDFRUULUUKFRZUUMMUUIUWD
      UULYDUUKFXTAUWBUWEXOXQXSVTPWAXTAXOXSTZYDUUKSZXOXQXSWBUWFAUWITZUWJULCDXPXR
      SZUWCUWKUWEUWJUWLUWBUWIAUWLXQXSXOXPXRHUJUMUMUWLUWDYDUULUUKXPXRXNGUNXPXRXN
      GUSUPUQPVRWAUOUUIUUSUULUUTQUUKUUTQUWHUUMSUVCUUIUULHUUTUUIHHWCZHGWFZXQXOUU
      LHQAUWNXTKVCZUVGUVMXPXNHHHGWDVHUVEVEUUIUUKHUUTUUIUWNXSXOUUKHQUWOUVDUVMXRX
      NHHHGWDVHUVEVEUULUUKFUUTUVIVGVHWGWHVRVRVRWANAXQTZEXPGRZXPEGRZXPEHQZUWPUWQ
      UWRSZAUWSXQNVCUWPUWEULUWPUWTULBEHXNESZUWEUWTUWPUXAUWDUWQUULUWRXNEXPGUSXNE
      XPGUNUPWIXOUWPUWEAXOXQUWEPWJWKWLWMOWNOWOAXMUWECUUTWPBUUTWPZAUWEBCUUTUUTAX
      NUUTQZUVBTZUWBUWEAUWBUXDAXOUXCXQUVBAHUUTXNJWQAHUUTXPJWQWRWSPVSWTAUUSGXGQZ
      XMUXBXAIAUWNUWMXGQZUXEKAHXGQZUXGUXFAHUUTXGJAUUSUUTXGQIFVAXBVNXCZUXHHHXGXG
      XDXEUWMHXGGXHXEVAXGFGBCXFXEXIXLXJXK $.
      $( [23-Dec-2013] $) $( [20-Jun-2011] $)
  $}

  $( A commutative ring is a ring.  (Contributed by Jeff Madsen,
     10-Jun-2010.) $)
  crngrng $p |- ( R e. CRing -> R e. RingOps ) $=
    ( ccring wcel cring ccm2 iscring simplbi ) ABCADCAECAFG $.
    $( [10-Jun-2010] $)

  ${
    $d X x y $.  $d A x y $.  $d B y $.  $d H x y $.  $d R x y $.
    crngcom.1 $e |- G = ( 1st ` R ) $.
    crngcom.2 $e |- H = ( 2nd ` R ) $.
    crngcom.3 $e |- X = ran G $.
    $( The multiplication operation of a commutative ring is commutative.
       (Contributed by Jeff Madsen, 8-Jun-2010.) $)
    crngcom $p |- ( ( R e. CRing /\ A e. X /\ B e. X )
                                        -> ( A H B ) = ( B H A ) ) $=
      ( vx vy ccring wcel co wceq cv wral opreq1 opreq2 eqeq12d simprbi rcla42v
      wa cring iscring2 mpan9 3impb ) CLMZAFMZBFMZABENZBAENZOZUHJPZKPZENZUOUNEN
      ZOZKFQJFQZUIUJUCUMUHCUDMUSJKCDEFGHIUEUAURUMAUOENZUOAENZOJKABFFUNAOUPUTUQV
      AUNAUOERUNAUOESTUOBOUTUKVAULUOBAESUOBAERTUBUFUG $.
      $( [8-Jun-2010] $)
  $}

  ${
    crngm.1 $e |- G = ( 1st ` R ) $.
    crngm.2 $e |- H = ( 2nd ` R ) $.
    crngm.3 $e |- X = ran G $.
    $( Commutative/associative law for commutative rings.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    crngm23 $p |- ( ( R e. CRing /\ ( A e. X /\ B e. X /\ C e. X ) )
                                -> ( ( A H B ) H C ) = ( ( A H C ) H B ) ) $=
      ( ccring wcel w3a wa co wceq crngcom 3adant3r1 ringoass sylan cring 3exp2
      opreq2d crngrng com34 3imp2 3eqtr4d ) DKLZAGLZBGLZCGLZMZNZABCFOZFOZACBFOZ
      FOZABFOCFOZACFOBFOZUMUNUPAFUHUJUKUNUPPUIBCDEFGHIJQRUCUHDUALZULURUOPDUDZAB
      CDEFGHIJSTUHUTULUSUQPZVAUTUIUJUKVBUTUIUKUJVBUTUIUKUJVBACBDEFGHIJSUBUEUFTU
      G $.
      $( [19-Jun-2010] $)

    $( Commutative/associative law for commutative rings.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    crngm4 $p |- ( ( R e. CRing /\ ( A e. X /\ B e. X )
                                  /\ ( C e. X /\ D e. X ) ) ->
                  ( ( A H B ) H ( C H D ) ) = ( ( A H C ) H ( B H D ) ) ) $=
      ( wcel wa co wceq w3a adantrrr ringocl 3expb 3jca ccring crngm23 sylan2br
      df-3an opreq1d cring crngrng adantrr simprrl simprrr syldan sylan simprlr
      ringoass adantrlr 3eqtr3d 3impb ) EUALZAHLZBHLZMZCHLZDHLZMZABGNZCDGNGNZAC
      GNZBDGNGNZOURVAVDMZMZVECGNZDGNZVGBGNZDGNZVFVHVJVKVMDGURVAVBVKVMOZVCVAVBMU
      RUSUTVBPVOUSUTVBUDABCEFGHIJKUBUCQUEUREUFLZVIVLVFOZEUGZVPVIVEHLZVBVCPVQVPV
      IMZVSVBVCVPVAVSVDVPUSUTVSABEFGHIJKRSUHVPVAVBVCUIVPVAVBVCUJZTVECDEFGHIJKUN
      UKULURVPVIVNVHOZVRVPVIVGHLZUTVCPWBVTWCUTVCVPVAVBWCVCVPUSVBWCUTVPUSVBWCACE
      FGHIJKRSUOQVPUSUTVDUMWATVGBDEFGHIJKUNUKULUPUQ $.
      $( [19-Jun-2010] $)
  $}

  $( A field is a commutative ring.  (Contributed by Jeff Madsen,
     8-Jun-2010.) $)
  fldcrng $p |- ( K e. Fld -> K e. CRing ) $=
    ( cdrng wcel ccm2 cring cfld ccring c2nd cfv c1st crn cgi csn cdif cxp cres
    wa cgr eqid drngi simpld anim1i df-fld eleq2i elin bitri iscring 3imtr4i
    cin ) ABCZADCZQZAECZUKQAFCZAGCUJUMUKUJUMAHIZAJIZKZUPLIZMNZUSOPRCAUPUOUQURUP
    SUOSUQSURSTUAUBUNABDUIZCULFUTAUCUDABDUEUFAUGUH $.
    $( [8-Jun-2010] $)

  $( The predicate "is a field".  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
  isfld2 $p |- ( K e. Fld <-> ( K e. DivRingOps /\ K e. CRing ) ) $=
    ( cfld wcel cdrng ccring wa flddivrng fldcrng jca cring iscring simprbi cin
    ccm2 elin biimpri df-fld syl6eleqr sylan2 impbii ) ABCZADCZAECZFUAUBUCAGAHI
    UCUBANCZUAUCAJCUDAKLUBUDFZADNMZBAUFCUEADNOPQRST $.
    $( [10-Jun-2010] $)

  ${
    $d R w x y z $.  $d S w x y z $.  $d X w x y z $.  $d Y w x y z $.
    $d F w x y z $.
    crnghomfo.1 $e |- G = ( 1st ` R ) $.
    crnghomfo.2 $e |- X = ran G $.
    crnghomfo.3 $e |- J = ( 1st ` S ) $.
    crnghomfo.4 $e |- Y = ran J $.
    $( The image of a homomorphism from a commutative ring is commutative.
       (Contributed by Jeff Madsen, 4-Jan-2011.) $)
    crnghomfo $p |- ( ( ( R e. CRing /\ S e. RingOps ) /\ ( F e. ( R RngHom S )
                                    /\ F : X -onto-> Y ) ) -> S e. CRing ) $=
      ( vy vz vw vx wcel wa co cfv wceq ccring cring crnghom wfo cv c2nd simplr
      wral wrex wi foelrn ex anim12d reeanv syl6ibr ad2antll eqid crngcom 3expb
      3ad2antl1 fveq2d crngrng rnghommul ancom2s 3eqtr3d opreq12 ancoms eqeq12d
      w3a syl3anl1 syl5ibrcom 3expa adantrr r19.23advv syld r19.21aivv iscring2
      sylanbrc ) AUAPZBUBPZQZCABUCRPZFGCUDZQZQZVTLUEZMUEZBUFSZRZWGWFWHRZTZMGUHL
      GUHBUAPVSVTWDUGWEWKLMGGWEWFGPZWGGPZQZWFNUEZCSZTZWGOUEZCSZTZQZOFUINFUIZWKW
      CWNXBUJWAWBWCWNWQNFUIZWTOFUIZQXBWCWLXCWMXDWCWLXCNFGWFCUKULWCWMXDOFGWGCUKU
      LUMWQWTNOFFUNUOUPWEXAWKNOFFWAWBWOFPZWRFPZQZXAWKUJZUJZWCVSVTWBXIVSVTWBVIZX
      GXHXJXGQZWKXAWPWSWHRZWSWPWHRZTXKWOWRAUFSZRZCSZWRWOXNRZCSZXLXMXKXOXQCVSVTX
      GXOXQTZWBVSXEXFXSWOWRADXNFHXNUQZIURUSUTVAVSAUBPZVTWBXGXPXLTAVBZWOWRABCDXN
      WHFHIXTWHUQZVCVJVSYAVTWBXGXRXMTZYBYAVTWBVIXFXEYDWRWOABCDXNWHFHIXTYCVCVDVJ
      VEXAWIXLWJXMWFWPWGWSWHVFWTWQWJXMTWGWSWFWPWHVFVGVHVKULVLVMVNVOVPLMBEWHGJYC
      KVQVR $.
      $( [4-Jan-2011] $)
  $}

