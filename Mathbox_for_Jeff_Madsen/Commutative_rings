$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Commutative rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CRing $.

  $( Extend class notation with the class of commutative rings. $)
  ccring $a class CRing $.

  $( Define the class of commutative rings. $)
  df-cring $a |- CRing = ( RingOps i^i Com2 ) $.

  $( The predicate "is a commutative ring".  (Contributed by Jeff Madsen,
     8-Jun-2010.) $)
  iscring $p |- ( R e. CRing <-> ( R e. RingOps /\ R e. Com2 ) ) $=
    ( ccring wcel cring ccm2 cin wa df-cring eleq2i elin bitri ) ABCADEFZCADCAE
    CGBLAHIADEJK $.
    $( [8-Jun-2010] $)

  ${
    $d R x y $.  $d X x y $.
    iscring2.1 $e |- G = ( 1st ` R ) $.
    iscring2.2 $e |- H = ( 2nd ` R ) $.
    iscring2.3 $e |- X = ran G $.
    $( The predicate "is a commutative ring".  (Contributed by Jeff Madsen,
       8-Jun-2010.) $)
    iscring2 $p |- ( R e. CRing <-> ( R e. RingOps /\
                          A. x e. X A. y e. X ( x H y ) = ( y H x ) ) ) $=
      ( ccring wcel cring ccm2 wa cv co wceq wral iscring c1st cfv c2nd cop wb
      wrel relrng 1st2nd mpan eleq1 crn rneqi eqtri raleqi opreqi eqeq12i
      ralbii bitri cvv fvex iscom2 mp2an 3bitr4ri syl6bb syl pm5.32i ) CJKCLKZC
      MKZNVFAOZBOZEPZVIVHEPZQZBFRZAFRZNCSVFVGVNVFCCTUAZCUBUAZUCZQZVGVNUDLUEVFVR
      UFCLUGUHVRVGVQMKZVNCVQMUIVHVIVPPZVIVHVPPZQZBVOUJZRZAFRWDAWCRZVNVSWDAFWCFD
      UJWCIDVOGUKULZUMVMWDAFVMWBBFRWDVLWBBFVJVTVKWAEVPVHVIHUNEVPVIVHHUNUOUPWBBF
      WCWFUMUQUPVOURKVPURKVSWEUDCTUSCUBUSURURVOVPABUTVAVBVCVDVEUQ $.
      $( [8-Jun-2010] $)
  $}

  ${
    $d ph w x y z $.  $d G w x y z $.  $d H w x y z $.  $d X w x y z $.
    $d U x y $.
    iscringd.1 $e |- ( ph -> G e. AbelOp ) $.
    iscringd.2 $e |- ( ph -> X = ran G ) $.
    iscringd.3 $e |- ( ph -> H : ( X X. X ) --> X ) $.
    iscringd.4 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                                  -> ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $.
    iscringd.5 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $.
    iscringd.6 $e |- ( ph -> U e. X ) $.
    iscringd.7 $e |- ( ( ph /\ y e. X ) -> ( y H U ) = y ) $.
    iscringd.8 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                                              -> ( x H y ) = ( y H x ) ) $.
    $( Conditions that determine a commutative ring.  (Contributed by Jeff
       Madsen, 20-Jun-2011.)  (Revised by Mario Carneiro, 23-Dec-2013.) $)
    iscringd $p |- ( ph -> <. G , H >. e. CRing ) $=
      ( vw cop cring wcel ccm2 ccring cv w3a co wceq id 3com13 wa wi eleq1
      3anbi1d anbi2d opreq2 opreq12d eqeq12d imbi12d 3anbi3d opreq1 opreq1d
      cabl crn adantr simpr3 eleqtrd simpr2 eqid ablocom syl111anc simpr1 cgr
      ablogrpo syl grpocl eleqtrrd jca oprex chvarv vtocl syldan 3simpa sylan2
      3simpb cxp wf foprrn 3eqtrd 3eqtr2d imbi2d an12s ex vtoclga mpcom eqtrd
      isringod wral eleq2d anbi12d biimpar r19.21aivva cvv wb rnexg eqeltrd
      xpexg syl11anc fex iscom2 mpbird iscring sylanbrc ) AFGRZSTXLUATZXLUBTABC
      DEFGHIJKLMBUCZHTZCUCZHTZDUCZHTZUDZAXSXQXOUDZXNXPFUEZXRGUEZXNXRGUEZXPXRGUE
      ZFUEZUFZXSXQXOYAYAUGUHAQUCZHTZXQXOUDZUIZYBYHGUEZXNYHGUEZXPYHGUEZFUEZUFZUJ
      ZAYAUIZYGUJQDYHXRUFZYKYRYPYGYSYJYAAYSYIXSXQXOYHXRHUKULUMYSYLYCYOYFYHXRYBG
      UNYSYMYDYNYEFYHXRXNGUNYHXRXPGUNUOUPUQAYIXQXSUDZUIZXRXPFUEZYHGUEZXRYHGUEZY
      NFUEZUFZUJZYQDBXRXNUFZUUAYKUUFYPUUHYTYJAUUHXSXOYIXQXRXNHUKURUMUUHUUCYLUUE
      YOUUHUUBYBYHGXRXNXPFUSUTUUHUUDYMYNFXRXNYHGUSUTUPUQAXTUIZUUBXNGUEZXRXNGUEZ
      XPXNGUEZFUEZUFZUJUUGBQXNYHUFZUUIUUAUUNUUFUUOXTYTAUUOXOYIXQXSXNYHHUKULUMUU
      OUUJUUCUUMUUEXNYHUUBGUNUUOUUKUUDUULYNFXNYHXRGUNXNYHXPGUNUOUPUQUUIUUJXPXRF
      UEZXNGUEZXNUUPGUEZUUMUUIUUBUUPXNGUUIFVATZXRFVBZTZXPUUTTZUUBUUPUFAUUSXTIVC
      ZUUIXRHUUTAXOXQXSVDZAHUUTUFXTJVCZVEZUUIXPHUUTAXOXQXSVFZUVEVEZXRXPFUUTUUTV
      GZVHVIUTAXTXOUUPHTZUIZUURUUQUFZUUIXOUVJAXOXQXSVJZUUIUUPUUTHUUIFVKTZUVBUVA
      UUPUUTTUUIUUSUVNUVCFVLVMUVHUVFXPXRFUUTUVIVNVIUVEVOVPAXOYIUIZUIZYMYHXNGUEZ
      UFZUJZAUVKUIZUVLUJQUUPXPXRFVQYHUUPUFZUVPUVTUVRUVLUWAUVOUVKAUWAYIUVJXOYHUU
      PHUKUMUMUWAYMUURUVQUUQYHUUPXNGUNYHUUPXNGUSUPUQAXOXQUIZUIZXNXPGUEZUULUFZUJ
      ZUVSCQXPYHUFZUWCUVPUWEUVRUWGUWBUVOAUWGXQYIXOXPYHHUKUMUMUWGUWDYMUULUVQXPYH
      XNGUNXPYHXNGUSUPUQPVRVSVTUUIUURUWDYDFUEUULUUKFUEZUUMMUUIUWDUULYDUUKFXTAUW
      BUWEXOXQXSWAPWBXTAXOXSUIZYDUUKUFZXOXQXSWCUWFAUWIUIZUWJUJCDXPXRUFZUWCUWKUW
      EUWJUWLUWBUWIAUWLXQXSXOXPXRHUKUMUMUWLUWDYDUULUUKXPXRXNGUNXPXRXNGUSUPUQPVR
      WBUOUUIUUSUULUUTTUUKUUTTUWHUUMUFUVCUUIUULHUUTUUIHHWDZHGWEZXQXOUULHTAUWNXT
      KVCZUVGUVMXPXNHHHGWFVIUVEVEUUIUUKHUUTUUIUWNXSXOUUKHTUWOUVDUVMXRXNHHHGWFVI
      UVEVEUULUUKFUUTUVIVHVIWGWHVRVRVRWBNAXQUIZEXPGUEZXPEGUEZXPEHTZUWPUWQUWRUFZ
      AUWSXQNVCUWPUWEUJUWPUWTUJBEHXNEUFZUWEUWTUWPUXAUWDUWQUULUWRXNEXPGUSXNEXPGU
      NUPWIXOUWPUWEAXOXQUWEPWJWKWLWMOWNOWOAXMUWECUUTWPBUUTWPZAUWEBCUUTUUTAXNUUT
      TZUVBUIZUWBUWEAUWBUXDAXOUXCXQUVBAHUUTXNJWQAHUUTXPJWQWRWSPVTWTAUUSGXATZXMU
      XBXBIAUWNUWMXATZUXEKAHXATZUXGUXFAHUUTXAJAUUSUUTXATIFVAXCVMXDZUXHHHXAXAXEX
      FUWMHXAGXGXFVAXAFGBCXHXFXIXLXJXK $.
      $( [23-Dec-2013] $) $( [20-Jun-2011] $)
  $}

  $( A commutative ring is a ring.  (Contributed by Jeff Madsen,
     10-Jun-2010.) $)
  crngrng $p |- ( R e. CRing -> R e. RingOps ) $=
    ( ccring wcel cring ccm2 iscring simplbi ) ABCADCAECAFG $.
    $( [10-Jun-2010] $)

  ${
    $d X x y $.  $d A x y $.  $d B y $.  $d H x y $.  $d R x y $.
    crngcom.1 $e |- G = ( 1st ` R ) $.
    crngcom.2 $e |- H = ( 2nd ` R ) $.
    crngcom.3 $e |- X = ran G $.
    $( The multiplication operation of a commutative ring is commutative.
       (Contributed by Jeff Madsen, 8-Jun-2010.) $)
    crngcom $p |- ( ( R e. CRing /\ A e. X /\ B e. X )
                                        -> ( A H B ) = ( B H A ) ) $=
      ( vx vy ccring wcel co wceq cv wral wa cring iscring2 simprbi opreq1
      opreq2 eqeq12d rcla42v mpan9 3impb ) CLMZAFMZBFMZABENZBAENZOZUHJPZKPZENZU
      OUNENZOZKFQJFQZUIUJRUMUHCSMUSJKCDEFGHITUAURUMAUOENZUOAENZOJKABFFUNAOUPUTU
      QVAUNAUOEUBUNAUOEUCUDUOBOUTUKVAULUOBAEUCUOBAEUBUDUEUFUG $.
      $( [8-Jun-2010] $)
  $}

  ${
    crngm.1 $e |- G = ( 1st ` R ) $.
    crngm.2 $e |- H = ( 2nd ` R ) $.
    crngm.3 $e |- X = ran G $.
    $( Commutative/associative law for commutative rings.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    crngm23 $p |- ( ( R e. CRing /\ ( A e. X /\ B e. X /\ C e. X ) )
                                -> ( ( A H B ) H C ) = ( ( A H C ) H B ) ) $=
      ( ccring wcel w3a wa co wceq crngcom 3adant3r1 opreq2d cring crngrng
      ringoass sylan 3exp2 com34 3imp2 3eqtr4d ) DKLZAGLZBGLZCGLZMZNZABCFOZFOZA
      CBFOZFOZABFOCFOZACFOBFOZUMUNUPAFUHUJUKUNUPPUIBCDEFGHIJQRSUHDTLZULURUOPDUA
      ZABCDEFGHIJUBUCUHUTULUSUQPZVAUTUIUJUKVBUTUIUKUJVBUTUIUKUJVBACBDEFGHIJUBUD
      UEUFUCUG $.
      $( [19-Jun-2010] $)

    $( Commutative/associative law for commutative rings.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    crngm4 $p |- ( ( R e. CRing /\ ( A e. X /\ B e. X )
                                  /\ ( C e. X /\ D e. X ) ) ->
                  ( ( A H B ) H ( C H D ) ) = ( ( A H C ) H ( B H D ) ) ) $=
      ( ccring wcel wa co wceq w3a df-3an crngm23 sylan2br adantrrr opreq1d
      cring crngrng ringocl 3expb adantrr simprrl simprrr 3jca ringoass syldan
      sylan adantrlr simprlr 3eqtr3d 3impb ) ELMZAHMZBHMZNZCHMZDHMZNZABGOZCDGOG
      OZACGOZBDGOGOZPURVAVDNZNZVECGOZDGOZVGBGOZDGOZVFVHVJVKVMDGURVAVBVKVMPZVCVA
      VBNURUSUTVBQVOUSUTVBRABCEFGHIJKSTUAUBUREUCMZVIVLVFPZEUDZVPVIVEHMZVBVCQVQV
      PVINZVSVBVCVPVAVSVDVPUSUTVSABEFGHIJKUEUFUGVPVAVBVCUHVPVAVBVCUIZUJVECDEFGH
      IJKUKULUMURVPVIVNVHPZVRVPVIVGHMZUTVCQWBVTWCUTVCVPVAVBWCVCVPUSVBWCUTVPUSVB
      WCACEFGHIJKUEUFUNUAVPUSUTVDUOWAUJVGBDEFGHIJKUKULUMUPUQ $.
      $( [19-Jun-2010] $)
  $}

  $( A field is a commutative ring.  (Contributed by Jeff Madsen,
     8-Jun-2010.) $)
  fldcrng $p |- ( K e. Fld -> K e. CRing ) $=
    ( cdrng wcel ccm2 wa cring cfld ccring c2nd cfv c1st crn cgi csn cdif cxp
    cres cgr eqid drngi simpld anim1i cin df-fld eleq2i elin bitri iscring
    3imtr4i ) ABCZADCZEZAFCZUKEAGCZAHCUJUMUKUJUMAIJZAKJZLZUPMJZNOZUSPQRCAUPUOUQ
    URUPSUOSUQSURSTUAUBUNABDUCZCULGUTAUDUEABDUFUGAUHUI $.
    $( [8-Jun-2010] $)

  $( The predicate "is a field".  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
  isfld2 $p |- ( K e. Fld <-> ( K e. DivRingOps /\ K e. CRing ) ) $=
    ( cfld wcel cdrng ccring wa flddivrng fldcrng jca ccm2 cring iscring
    simprbi cin elin biimpri df-fld syl6eleqr sylan2 impbii ) ABCZADCZAECZFUAUB
    UCAGAHIUCUBAJCZUAUCAKCUDALMUBUDFZADJNZBAUFCUEADJOPQRST $.
    $( [10-Jun-2010] $)

  ${
    $d R w x y z $.  $d S w x y z $.  $d X w x y z $.  $d Y w x y z $.
    $d F w x y z $.
    crnghomfo.1 $e |- G = ( 1st ` R ) $.
    crnghomfo.2 $e |- X = ran G $.
    crnghomfo.3 $e |- J = ( 1st ` S ) $.
    crnghomfo.4 $e |- Y = ran J $.
    $( The image of a homomorphism from a commutative ring is commutative.
       (Contributed by Jeff Madsen, 4-Jan-2011.) $)
    crnghomfo $p |- ( ( ( R e. CRing /\ S e. RingOps ) /\ ( F e. ( R RngHom S )
                                    /\ F : X -onto-> Y ) ) -> S e. CRing ) $=
      ( vy vz vw vx ccring wcel cring wa crnghom co wfo cv c2nd cfv wceq wral
      simplr wrex wi foelrn ex anim12d reeanv syl6ibr ad2antll w3a eqid
      crngcom 3expb 3ad2antl1 fveq2d crngrng rnghommul syl3anl1 ancom2s
      3eqtr3d opreq12 ancoms eqeq12d syl5ibrcom 3expa adantrr r19.23advv syld
      r19.21aivv iscring2 sylanbrc ) APQZBRQZSZCABTUAQZFGCUBZSZSZVTLUCZMUCZBUDU
      EZUAZWGWFWHUAZUFZMGUGLGUGBPQVSVTWDUHWEWKLMGGWEWFGQZWGGQZSZWFNUCZCUEZUFZWG
      OUCZCUEZUFZSZOFUINFUIZWKWCWNXBUJWAWBWCWNWQNFUIZWTOFUIZSXBWCWLXCWMXDWCWLXC
      NFGWFCUKULWCWMXDOFGWGCUKULUMWQWTNOFFUNUOUPWEXAWKNOFFWAWBWOFQZWRFQZSZXAWKU
      JZUJZWCVSVTWBXIVSVTWBUQZXGXHXJXGSZWKXAWPWSWHUAZWSWPWHUAZUFXKWOWRAUDUEZUAZ
      CUEZWRWOXNUAZCUEZXLXMXKXOXQCVSVTXGXOXQUFZWBVSXEXFXSWOWRADXNFHXNURZIUSUTVA
      VBVSARQZVTWBXGXPXLUFAVCZWOWRABCDXNWHFHIXTWHURZVDVEVSYAVTWBXGXRXMUFZYBYAVT
      WBUQXFXEYDWRWOABCDXNWHFHIXTYCVDVFVEVGXAWIXLWJXMWFWPWGWSWHVHWTWQWJXMUFWGWS
      WFWPWHVHVIVJVKULVLVMVNVOVPLMBEWHGJYCKVQVR $.
      $( [4-Jan-2011] $)
  $}

