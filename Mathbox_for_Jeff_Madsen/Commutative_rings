$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Commutative rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CRing $.

  $( Extend class notation with the class of commutative rings. $)
  ccring $a class CRing $.

  $( Define the class of commutative rings. $)
  df-cring $a |- CRing = ( RingOps i^i Com2 ) $.

  $( The predicate "is a commutative ring". $)
  iscring $p |- ( R e. CRing <-> ( R e. RingOps /\ R e. Com2 ) ) $=
    ( ccring wcel cring ccm2 cin wa df-cring eleq2i elin bitri ) ABCADEFZCADCAE
    CGBLAHIADEJK $.
    $( [8-Jun-2010] $)

  ${
    $d R x y $.  $d X x y $.
    iscring2.1 $e |- G = ( 1st ` R ) $.
    iscring2.2 $e |- H = ( 2nd ` R ) $.
    iscring2.3 $e |- X = ran G $.
    $( The predicate "is a commutative ring". $)
    iscring2 $p |- ( R e. CRing <-> ( R e. RingOps /\
                          A. x e. X A. y e. X ( x H y ) = ( y H x ) ) ) $=
      ( ccring wcel cring ccm2 wa cv co wceq wral iscring c1st cfv c2nd cop wb
      wrel relrng 1st2nd mpan eleq1 crn rneqi eqtri raleqi opreqi eqeq12i
      ralbii bitri cvv fvex iscom2 mp2an 3bitr4ri syl6bb syl pm5.32i ) CJKCLKZC
      MKZNVFAOZBOZEPZVIVHEPZQZBFRZAFRZNCSVFVGVNVFCCTUAZCUBUAZUCZQZVGVNUDLUEVFVR
      UFCLUGUHVRVGVQMKZVNCVQMUIVHVIVPPZVIVHVPPZQZBVOUJZRZAFRWDAWCRZVNVSWDAFWCFD
      UJWCIDVOGUKULZUMVMWDAFVMWBBFRWDVLWBBFVJVTVKWAEVPVHVIHUNEVPVIVHHUNUOUPWBBF
      WCWFUMUQUPVOURKVPURKVSWEUDCTUSCUBUSURURVOVPABUTVAVBVCVDVEUQ $.
      $( [8-Jun-2010] $)
  $}

  ${
    $d ph w x y z $.  $d G w x y z $.  $d H w x y z $.  $d R x y $.
    $d X w x y z $.  $d U x y $.
    iscringd.1 $e |- G = ( 1st ` R ) $.
    iscringd.2 $e |- H = ( 2nd ` R ) $.
    iscringd.3 $e |- ( ph -> R e. ( _V X. _V ) ) $.
    iscringd.4 $e |- ( ph -> G e. AbelOp ) $.
    iscringd.5 $e |- ( ph -> X = ran G ) $.
    iscringd.6 $e |- ( ph -> H : ( X X. X ) --> X ) $.
    iscringd.7 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                                  -> ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $.
    iscringd.8 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $.
    iscringd.9 $e |- ( ph -> U e. X ) $.
    iscringd.10 $e |- ( ( ph /\ y e. X ) -> ( y H U ) = y ) $.
    iscringd.11 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                                              -> ( x H y ) = ( y H x ) ) $.
    $( Conditions that determine a commutative ring. $)
    iscringd $p |- ( ph -> R e. CRing ) $=
      ( vw cring wcel cv co wceq crn wral ccring w3a wa 3anrot wi eleq1
      3anbi1d anbi2d opreq1 opreq12d eqeq12d imbi12d 3anbi3d opreq2 opreq2d
      3anbi2d opreq1d chvarv sylan2br eleq2d anbi12d biimpa cgr cabl ablogrpo
      syl eqid grpocl 3expb sylan wb adantr mpbird syldan 3adantr3 simpr3 jca
      oprex anbi1d vtocl 3adantr2 ancom2s eqcomd 3adantr1 3eqtr4d simprl
      vtoclg mpcom anass1rs mpdan eqtrd isringod ex sylbird r19.21aivv
      iscring2 sylanbrc ) AEUBUCBUDZCUDZHUEZXGXFHUEZUFZCGUGZUHBXKUHEUIUCABCDEFG
      HIJKLMNOPQAXFIUCZXGIUCZDUDZIUCZUJZUKZXNXFXGGUEZHUEZXNXFHUEZXNXGHUEZGUEZXR
      XNHUEZXFXNHUEZXGXNHUEZGUEXPAXOXLXMUJZXSYBUFZXOXLXMULAUAUDZIUCZXLXMUJZUKZY
      HXRHUEZYHXFHUEZYHXGHUEZGUEZUFZUMZAYFUKZYGUMUADYHXNUFZYKYRYPYGYSYJYFAYSYIX
      OXLXMYHXNIUNUOUPYSYLXSYOYBYHXNXRHUQYSYMXTYNYAGYHXNXFHUQYHXNXGHUQURUSUTAYI
      XLXOUJZUKZYHXFXNGUEZHUEZYMYHXNHUEZGUEZUFZUMZYQDCXNXGUFZUUAYKUUFYPUUHYTYJA
      UUHXOXMYIXLXNXGIUNVAUPUUHUUCYLUUEYOUUHUUBXRYHHXNXGXFGVBVCUUHUUDYNYMGXNXGY
      HHVBVCUSUTAYIXMXOUJZUKZYHXGXNGUEZHUEZYNUUDGUEZUFZUMZUUGCBXGXFUFZUUJUUAUUN
      UUFUUPUUIYTAUUPXMXLYIXOXGXFIUNVDUPUUPUULUUCUUMUUEUUPUUKUUBYHHXGXFXNGUQVCU
      UPYNYMUUDGXGXFYHHVBVEUSUTXQXFUUKHUEZXHYDGUEZUFZUMUUOBUAXFYHUFZXQUUJUUSUUN
      UUTXPUUIAUUTXLYIXMXOXFYHIUNZUOUPUUTUUQUULUURUUMXFYHUUKHUQUUTXHYNYDUUDGXFY
      HXGHUQZXFYHXNHUQURUSUTQVFVFVFVFVGAXPXRIUCZXOUKZYCXSUFZXQUVCXOAXLXMUVCXOAX
      LXMUKZXFXKUCZXGXKUCZUKZUVCAUVFUVIAXLUVGXMUVHAIXKXFNVHAIXKXGNVHVIZVJAUVIUK
      UVCXRXKUCZAGVKUCZUVIUVKAGVLUCUVLMGVMVNUVLUVGUVHUVKXFXGGXKXKVOZVPVQVRAUVCU
      VKVSUVIAIXKXRNVHVTWAWBWCAXLXMXOWDWEAYIXOUKZUKZUUDXNYHHUEZUFZUMZAUVDUKZUVE
      UMUAXRXFXGGWFYHXRUFZUVOUVSUVQUVEUVTUVNUVDAUVTYIUVCXOYHXRIUNWGUPUVTUUDYCUV
      PXSYHXRXNHUQYHXRXNHVBUSUTAYIXMUKZUKZYNXGYHHUEZUFZUMZUVRCDXGXNUFZUWBUVOUWD
      UVQUWFUWAUVNAUWFXMXOYIXGXNIUNZUPUPUWFYNUUDUWCUVPXGXNYHHVBXGXNYHHUQUSUTAUV
      FUKZXJUMZUWEBUAUUTUWHUWBXJUWDUUTUVFUWAAUUTXLYIXMUVAWGUPUUTXHYNXIUWCUVBXFY
      HXGHVBUSUTTVFVFWHWBXQYDXTYEYAGAXLXOYDXTUFZXMUWIAXLXOUKZUKZUWJUMCDUWFUWHUW
      LXJUWJUWFUVFUWKAUWFXMXOXLUWGUPUPUWFXHYDXIXTXGXNXFHVBXGXNXFHUQUSUTTVFWIAXM
      XOYEYAUFXLAXMXOUKUKYAYEAXOXMYAYEUFZUWIAXOXMUKZUKZUWMUMBDXFXNUFZUWHUWOXJUW
      MUWPUVFUWNAUWPXLXOXMXFXNIUNWGUPUWPXHYAXIYEXFXNXGHUQXFXNXGHVBUSUTTVFWJWKWL
      URWMRSAXMUKZFXGHUEZXGFHUEZXGUWQFIUCZUWRUWSUFZAUWTXMRVTAUWTXMUXAUWTAUWTXMU
      KZUKZUXAAUWTXMWNUWIUXCUXAUMBFIXFFUFZUWHUXCXJUXAUXDUVFUXBAUXDXLUWTXMXFFIUN
      WGUPUXDXHUWRXIUWSXFFXGHUQXFFXGHVBUSUTTWOWPWQWRSWSWTAXJBCXKXKAUVIUVFXJUVJA
      UVFXJTXAXBXCBCEGHXKJKUVMXDXE $.
      $( [20-Jun-2011] $)
  $}

  $( A commutative ring is a ring. $)
  crngrng $p |- ( R e. CRing -> R e. RingOps ) $=
    ( ccring wcel cring ccm2 iscring simplbi ) ABCADCAECAFG $.
    $( [10-Jun-2010] $)

  ${
    $d X x y $.  $d A x y $.  $d B y $.  $d H x y $.  $d R x y $.
    crngcom.1 $e |- G = ( 1st ` R ) $.
    crngcom.2 $e |- H = ( 2nd ` R ) $.
    crngcom.3 $e |- X = ran G $.
    $( The multiplication operation of a commutative ring is commutative. $)
    crngcom $p |- ( ( R e. CRing /\ A e. X /\ B e. X )
                                        -> ( A H B ) = ( B H A ) ) $=
      ( vx vy ccring wcel co wceq cv wral wa cring iscring2 simprbi opreq1
      opreq2 eqeq12d rcla42v mpan9 3impb ) CLMZAFMZBFMZABENZBAENZOZUHJPZKPZENZU
      OUNENZOZKFQJFQZUIUJRUMUHCSMUSJKCDEFGHITUAURUMAUOENZUOAENZOJKABFFUNAOUPUTU
      QVAUNAUOEUBUNAUOEUCUDUOBOUTUKVAULUOBAEUCUOBAEUBUDUEUFUG $.
      $( [8-Jun-2010] $)
  $}

  ${
    crngm.1 $e |- G = ( 1st ` R ) $.
    crngm.2 $e |- H = ( 2nd ` R ) $.
    crngm.3 $e |- X = ran G $.
    $( Commutative/associative law for commutative rings. $)
    crngm23 $p |- ( ( R e. CRing /\ ( A e. X /\ B e. X /\ C e. X ) )
                                -> ( ( A H B ) H C ) = ( ( A H C ) H B ) ) $=
      ( ccring wcel w3a wa co wceq crngcom 3adant3r1 opreq2d cring crngrng
      ringoass sylan 3exp2 com34 3imp2 3eqtr4d ) DKLZAGLZBGLZCGLZMZNZABCFOZFOZA
      CBFOZFOZABFOCFOZACFOBFOZUMUNUPAFUHUJUKUNUPPUIBCDEFGHIJQRSUHDTLZULURUOPDUA
      ZABCDEFGHIJUBUCUHUTULUSUQPZVAUTUIUJUKVBUTUIUKUJVBUTUIUKUJVBACBDEFGHIJUBUD
      UEUFUCUG $.
      $( [19-Jun-2010] $)

    $( Commutative/associative law for commutative rings. $)
    crngm4 $p |- ( ( R e. CRing /\ ( A e. X /\ B e. X )
                                  /\ ( C e. X /\ D e. X ) ) ->
                  ( ( A H B ) H ( C H D ) ) = ( ( A H C ) H ( B H D ) ) ) $=
      ( ccring wcel wa co wceq w3a df-3an crngm23 sylan2br adantrrr opreq1d
      cring crngrng ringocl 3expb adantrr simprrl simprrr 3jca ringoass syldan
      sylan adantrlr simprlr 3eqtr3d 3impb ) ELMZAHMZBHMZNZCHMZDHMZNZABGOZCDGOG
      OZACGOZBDGOGOZPURVAVDNZNZVECGOZDGOZVGBGOZDGOZVFVHVJVKVMDGURVAVBVKVMPZVCVA
      VBNURUSUTVBQVOUSUTVBRABCEFGHIJKSTUAUBUREUCMZVIVLVFPZEUDZVPVIVEHMZVBVCQVQV
      PVINZVSVBVCVPVAVSVDVPUSUTVSABEFGHIJKUEUFUGVPVAVBVCUHVPVAVBVCUIZUJVECDEFGH
      IJKUKULUMURVPVIVNVHPZVRVPVIVGHMZUTVCQWBVTWCUTVCVPVAVBWCVCVPUSVBWCUTVPUSVB
      WCACEFGHIJKUEUFUNUAVPUSUTVDUOWAUJVGBDEFGHIJKUKULUMUPUQ $.
      $( [19-Jun-2010] $)
  $}

  $( A field is a commutative ring. $)
  fldcrng $p |- ( K e. Fld -> K e. CRing ) $=
    ( cdrng wcel ccm2 wa cring cfld ccring c2nd cfv c1st crn cgi csn cdif cxp
    cres cgr eqid drngi simpld anim1i cin df-fld eleq2i elin bitri iscring
    3imtr4i ) ABCZADCZEZAFCZUKEAGCZAHCUJUMUKUJUMAIJZAKJZLZUPMJZNOZUSPQRCAUPUOUQ
    URUPSUOSUQSURSTUAUBUNABDUCZCULGUTAUDUEABDUFUGAUHUI $.
    $( [8-Jun-2010] $)

  $( The predicate "is a field". $)
  isfld2 $p |- ( K e. Fld <-> ( K e. DivRingOps /\ K e. CRing ) ) $=
    ( cfld wcel cdrng ccring wa flddivrng fldcrng jca ccm2 cring iscring
    simprbi cin elin biimpri df-fld syl6eleqr sylan2 impbii ) ABCZADCZAECZFUAUB
    UCAGAHIUCUBAJCZUAUCAKCUDALMUBUDFZADJNZBAUFCUEADJOPQRST $.
    $( [10-Jun-2010] $)

  ${
    $d R w x y z $.  $d S w x y z $.  $d X w x y z $.  $d Y w x y z $.
    $d F w x y z $.
    crnghomfo.1 $e |- G = ( 1st ` R ) $.
    crnghomfo.2 $e |- X = ran G $.
    crnghomfo.3 $e |- J = ( 1st ` S ) $.
    crnghomfo.4 $e |- Y = ran J $.
    $( The image of a homomorphism from a commutative ring is commutative. $)
    crnghomfo $p |- ( ( ( R e. CRing /\ S e. RingOps ) /\ ( F e. ( R RngHom S )
                                    /\ F : X -onto-> Y ) ) -> S e. CRing ) $=
      ( vy vz vw vx ccring wcel cring wa crnghom co wfo cv c2nd cfv wceq wral
      simplr wrex wi foelrn ex anim12d reeanv syl6ibr ad2antll w3a eqid
      crngcom 3expb 3ad2antl1 fveq2d crngrng rnghommul syl3anl1 ancom2s
      3eqtr3d opreq12 ancoms eqeq12d syl5ibrcom 3expa adantrr r19.23advv syld
      r19.21aivv iscring2 sylanbrc ) APQZBRQZSZCABTUAQZFGCUBZSZSZVTLUCZMUCZBUDU
      EZUAZWGWFWHUAZUFZMGUGLGUGBPQVSVTWDUHWEWKLMGGWEWFGQZWGGQZSZWFNUCZCUEZUFZWG
      OUCZCUEZUFZSZOFUINFUIZWKWCWNXBUJWAWBWCWNWQNFUIZWTOFUIZSXBWCWLXCWMXDWCWLXC
      NFGWFCUKULWCWMXDOFGWGCUKULUMWQWTNOFFUNUOUPWEXAWKNOFFWAWBWOFQZWRFQZSZXAWKU
      JZUJZWCVSVTWBXIVSVTWBUQZXGXHXJXGSZWKXAWPWSWHUAZWSWPWHUAZUFXKWOWRAUDUEZUAZ
      CUEZWRWOXNUAZCUEZXLXMXKXOXQCVSVTXGXOXQUFZWBVSXEXFXSWOWRADXNFHXNURZIUSUTVA
      VBVSARQZVTWBXGXPXLUFAVCZWOWRABCDXNWHFHIXTWHURZVDVEVSYAVTWBXGXRXMUFZYBYAVT
      WBUQXFXEYDWRWOABCDXNWHFHIXTYCVDVFVEVGXAWIXLWJXMWFWPWGWSWHVHWTWQWJXMUFWGWS
      WFWPWHVHVIVJVKULVLVMVNVOVPLMBEWHGJYCKVQVR $.
      $( [4-Jan-2011] $)
  $}

