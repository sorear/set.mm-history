$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Commutative rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CRing $.

  $( Extend class notation with the class of commutative rings. $)
  ccring $a class CRing $.

  $( Define the class of commutative rings. $)
  df-cring $a |- CRing = ( Ring i^i Com2 ) $.

  $( The predicate "is a commutative ring". $)
  iscring $p |- ( R e. CRing <-> ( R e. Ring /\ R e. Com2 ) ) $=
    ( ccring wcel cring ccm2 cin wa df-cring eleq2i elin bitri ) ABCADEFZCADCAE
    CGBLAHIADEJK $.
    $( [25-Jun-2011] $) $( [8-Jun-2010] $)

  ${
    $d R x y $.  $d X x y $.
    iscring2.1 $e |- G = ( 1st ` R ) $.
    iscring2.2 $e |- H = ( 2nd ` R ) $.
    iscring2.3 $e |- X = ran G $.
    $( The predicate "is a commutative ring". $)
    iscring2 $p |- ( R e. CRing <-> ( R e. Ring /\
                          A. x e. X A. y e. X ( x H y ) = ( y H x ) ) ) $=
      ( ccring wcel cring ccm2 wa cv co wceq wral iscring c1st cfv c2nd cop wb
      wrel relrng 1st2nd mpan eleq1 crn rneqi eqtri raleq1 ax-mp opreqi
      eqeq12i ralbii bitri cvv fvex iscom2 mp2an 3bitr4ri syl6bb syl pm5.32i )
      CJKCLKZCMKZNVGAOZBOZEPZVJVIEPZQZBFRZAFRZNCSVGVHVOVGCCTUAZCUBUAZUCZQZVHVOU
      DLUEVGVSUFCLUGUHVSVHVRMKZVOCVRMUIVIVJVQPZVJVIVQPZQZBVPUJZRZAFRZWEAWDRZVOV
      TFWDQZWFWGUDFDUJWDIDVPGUKULZWEAFWDUMUNVNWEAFVNWCBFRZWEVMWCBFVKWAVLWBEVQVI
      VJHUOEVQVJVIHUOUPUQWHWJWEUDWIWCBFWDUMUNURUQVPUSKVQUSKVTWGUDCTUTCUBUTUSUSV
      PVQABVAVBVCVDVEVFUR $.
      $( [25-Jun-2011] $) $( [8-Jun-2010] $)
  $}

  ${
    $d ph w x y z $.  $d G w x y z $.  $d H w x y z $.  $d R x y $.
    $d X w x y z $.  $d U x y $.
    iscringd.1 $e |- G = ( 1st ` R ) $.
    iscringd.2 $e |- H = ( 2nd ` R ) $.
    iscringd.3 $e |- ( ph -> R e. ( _V X. _V ) ) $.
    iscringd.4 $e |- ( ph -> G e. Abel ) $.
    iscringd.5 $e |- ( ph -> X = ran G ) $.
    iscringd.6 $e |- ( ph -> H : ( X X. X ) --> X ) $.
    iscringd.7 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                                  -> ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $.
    iscringd.8 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $.
    iscringd.9 $e |- ( ph -> U e. X ) $.
    iscringd.10 $e |- ( ( ph /\ y e. X ) -> ( y H U ) = y ) $.
    iscringd.11 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                                              -> ( x H y ) = ( y H x ) ) $.
    $( Conditions that determine a commutative ring. $)
    iscringd $p |- ( ph -> R e. CRing ) $=
      ( vw ccring wcel cring cv co wceq crn wral eqid iscring2 w3a wa wi eleq1
      3anbi1d anbi2d opreq1 opreq12d eqeq12d imbi12d 3anbi3d opreq2 opreq2d
      3anbi2d opreq1d chvarv 3anrot sylan2br eleq2d anbi12d biimpa cgr grpcl
      3expb cabl ablgrp syl sylan wb adantr mpbird syldan 3adantr3 3simpr3 jca
      oprex anbi1d vtocl 3adantr2 ancom2s eqcomd 3adantr1 3eqtr4d simprl
      vtoclg mpcom anass1rs mpdan eqtrd isringd ex sylbird r19.21aivv sylanbrc
      ) EUBUCEUDUCBUEZCUEZHUFZXGXFHUFZUGZCGUHZUIBXKUIABCEGHXKJKXKUJZUKABCDEFGHI
      JKLMNOPQAXFIUCZXGIUCZDUEZIUCZULZUMZXOXFXGGUFZHUFZXOXFHUFZXOXGHUFZGUFZXSXO
      HUFZXFXOHUFZXGXOHUFZGUFAXPXMXNULZXTYCUGZXQAUAUEZIUCZXMXNULZUMZYIXSHUFZYIX
      FHUFZYIXGHUFZGUFZUGZUNZAYGUMZYHUNUADYIXOUGZYLYSYQYHYTYKYGAYTYJXPXMXNYIXOI
      UOUPUQYTYMXTYPYCYIXOXSHURYTYNYAYOYBGYIXOXFHURYIXOXGHURUSUTVAAYJXMXPULZUMZ
      YIXFXOGUFZHUFZYNYIXOHUFZGUFZUGZUNZYRDCXOXGUGZUUBYLUUGYQUUIUUAYKAUUIXPXNYJ
      XMXOXGIUOVBUQUUIUUDYMUUFYPUUIUUCXSYIHXOXGXFGVCVDUUIUUEYOYNGXOXGYIHVCVDUTV
      AAYJXNXPULZUMZYIXGXOGUFZHUFZYOUUEGUFZUGZUNZUUHCBXGXFUGZUUKUUBUUOUUGUUQUUJ
      UUAAUUQXNXMYJXPXGXFIUOVEUQUUQUUMUUDUUNUUFUUQUULUUCYIHXGXFXOGURVDUUQYOYNUU
      EGXGXFYIHVCVFUTVAXRXFUULHUFZXHYEGUFZUGZUNUUPBUAXFYIUGZXRUUKUUTUUOUVAXQUUJ
      AUVAXMYJXNXPXFYIIUOZUPUQUVAUURUUMUUSUUNXFYIUULHURUVAXHYOYEUUEGXFYIXGHURZX
      FYIXOHURUSUTVAQVGVGVGVGXPXMXNVHVIAXQXSIUCZXPUMZYDXTUGZXRUVDXPAXMXNUVDXPAX
      MXNUMZXFXKUCZXGXKUCZUMZUVDAUVGUVJAXMUVHXNUVIAIXKXFNVJAIXKXGNVJVKZVLAUVJUM
      UVDXSXKUCZGVMUCZUVJUVLAUVMUVHUVIUVLXFXGGXKXLVNVOAGVPUCUVMMGVQVRVSAUVDUVLV
      TUVJAIXKXSNVJWAWBWCWDAXMXNXPWEWFAYJXPUMZUMZUUEXOYIHUFZUGZUNZAUVEUMZUVFUNU
      AXSXFXGGWGYIXSUGZUVOUVSUVQUVFUVTUVNUVEAUVTYJUVDXPYIXSIUOWHUQUVTUUEYDUVPXT
      YIXSXOHURYIXSXOHVCUTVAAYJXNUMZUMZYOXGYIHUFZUGZUNZUVRCDXGXOUGZUWBUVOUWDUVQ
      UWFUWAUVNAUWFXNXPYJXGXOIUOZUQUQUWFYOUUEUWCUVPXGXOYIHVCXGXOYIHURUTVAAUVGUM
      ZXJUNZUWEBUAUVAUWHUWBXJUWDUVAUVGUWAAUVAXMYJXNUVBWHUQUVAXHYOXIUWCUVCXFYIXG
      HVCUTVATVGVGWIWCXRYEYAYFYBGAXMXPYEYAUGZXNUWIAXMXPUMZUMZUWJUNCDUWFUWHUWLXJ
      UWJUWFUVGUWKAUWFXNXPXMUWGUQUQUWFXHYEXIYAXGXOXFHVCXGXOXFHURUTVATVGWJAXNXPY
      FYBUGXMAXNXPUMUMYBYFAXPXNYBYFUGZUWIAXPXNUMZUMZUWMUNBDXFXOUGZUWHUWOXJUWMUW
      PUVGUWNAUWPXMXPXNXFXOIUOWHUQUWPXHYBXIYFXFXOXGHURXFXOXGHVCUTVATVGWKWLWMUSW
      NRSAXNUMZFXGHUFZXGFHUFZXGUWQFIUCZUWRUWSUGZAUWTXNRWAAUWTXNUXAUWTAUWTXNUMZU
      MZUXAAUWTXNWOUWIUXCUXAUNBFIXFFUGZUWHUXCXJUXAUXDUVGUXBAUXDXMUWTXNXFFIUOWHU
      QUXDXHUWRXIUWSXFFXGHURXFFXGHVCUTVATWPWQWRWSSWTXAAXJBCXKXKAUVJUVGXJUVKAUVG
      XJTXBXCXDXE $.
      $( [25-Jun-2011] $) $( [20-Jun-2011] $)
  $}

  $( A commutative ring is a ring. $)
  crngrng $p |- ( R e. CRing -> R e. Ring ) $=
    ( ccring wcel cring ccm2 iscring pm3.26bi ) ABCADCAECAFG $.
    $( [25-Jun-2011] $) $( [10-Jun-2010] $)

  ${
    $d X x y $.  $d A x y $.  $d B y $.  $d H x y $.  $d R x y $.
    crngcom.1 $e |- G = ( 1st ` R ) $.
    crngcom.2 $e |- H = ( 2nd ` R ) $.
    crngcom.3 $e |- X = ran G $.
    $( The multiplication operation of a commutative ring is commutative. $)
    crngcom $p |- ( ( R e. CRing /\ A e. X /\ B e. X )
                                        -> ( A H B ) = ( B H A ) ) $=
      ( vx vy ccring wcel co wceq cv wral wa cring iscring2 pm3.27bi opreq1
      opreq2 eqeq12d rcla42v mpan9 3impb ) CLMZAFMZBFMZABENZBAENZOZUHJPZKPZENZU
      OUNENZOZKFQJFQZUIUJRUMUHCSMUSJKCDEFGHITUAURUMAUOENZUOAENZOJKABFFUNAOUPUTU
      QVAUNAUOEUBUNAUOEUCUDUOBOUTUKVAULUOBAEUCUOBAEUBUDUEUFUG $.
      $( [25-Jun-2011] $) $( [8-Jun-2010] $)
  $}

  ${
    crngm.1 $e |- G = ( 1st ` R ) $.
    crngm.2 $e |- H = ( 2nd ` R ) $.
    crngm.3 $e |- X = ran G $.

    $( Commutative/associative law for commutative rings. $)
    crngm23 $p |- ( ( R e. CRing /\ ( A e. X /\ B e. X /\ C e. X ) )
                                -> ( ( A H B ) H C ) = ( ( A H C ) H B ) ) $=
      ( ccring wcel w3a wa co wceq crngcom 3adant3r1 opreq2d cring ringass
      crngrng sylan 3exp2 com34 3imp2 3eqtr4d ) DKLZAGLZBGLZCGLZMZNZABCFOZFOZAC
      BFOZFOZABFOCFOZACFOBFOZUMUNUPAFUHUJUKUNUPPUIBCDEFGHIJQRSDTLZULURUOPUHABCD
      EFGHIJUADUBZUCUTULUSUQPZUHUTUIUJUKVBUTUIUKUJVBUTUIUKUJVBACBDEFGHIJUAUDUEU
      FVAUCUG $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)

    $( Commutative/associative law for commutative rings. $)
    crngm4 $p |- ( ( R e. CRing /\ ( A e. X /\ B e. X )
                                  /\ ( C e. X /\ D e. X ) ) ->
                  ( ( A H B ) H ( C H D ) ) = ( ( A H C ) H ( B H D ) ) ) $=
      ( ccring wcel wa co wceq w3a crngm23 df-3an sylan2br adantrrr opreq1d
      cring ringcl 3expb adantrr simprrl simprrr 3jca ringass syldan crngrng
      sylan adantrlr simprlr 3eqtr3d 3impb ) ELMZAHMZBHMZNZCHMZDHMZNZABGOZCDGOG
      OZACGOZBDGOGOZPURVAVDNZNZVECGOZDGOZVGBGOZDGOZVFVHVJVKVMDGURVAVBVKVMPZVCUR
      USUTVBQVOVAVBNABCEFGHIJKRUSUTVBSTUAUBEUCMZVIVLVFPZURVPVIVEHMZVBVCQVQVPVIN
      ZVRVBVCVPVAVRVDVPUSUTVRABEFGHIJKUDUEUFVPVAVBVCUGVPVAVBVCUHZUIVECDEFGHIJKU
      JUKEULZUMVPVIVNVHPZURVPVIVGHMZUTVCQWBVSWCUTVCVPVAVBWCVCVPUSVBWCUTVPUSVBWC
      ACEFGHIJKUDUEUNUAVPUSUTVDUOVTUIVGBDEFGHIJKUJUKWAUMUPUQ $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)
  $}

  $( A field is a commutative ring. $)
  fldcrng $p |- ( K e. Fld -> K e. CRing ) $=
    ( cdrng wcel ccm2 wa cring cfld ccring c2nd cfv c1st crn cgi csn cdif cxp
    cres cgr eqid drngi pm3.26d anim1i cin df-fld eleq2i elin bitri iscring
    3imtr4i ) ABCZADCZEZAFCZUKEAGCZAHCUJUMUKUJUMAIJZAKJZLZUPMJZNOZUSPQRCAUPUOUQ
    URUPSUOSUQSURSTUAUBUNABDUCZCULGUTAUDUEABDUFUGAUHUI $.
    $( [25-Jun-2011] $) $( [8-Jun-2010] $)

  $( The predicate "is a field". $)
  isfld2 $p |- ( K e. Fld <-> ( K e. DivRing /\ K e. CRing ) ) $=
    ( cfld wcel cdrng ccring wa flddivrng fldcrng jca ccm2 cin elin biimpri
    df-fld syl6eleqr cring iscring pm3.27bi sylan2 impbii ) ABCZADCZAECZFUAUBUC
    AGAHIUBAJCZUAUCUBUDFZADJKZBAUFCUEADJLMNOUCAPCUDAQRST $.
    $( [25-Jun-2011] $) $( [10-Jun-2010] $)

  ${
    $d R w x y z $.  $d S w x y z $.  $d X w x y z $.  $d Y w x y z $.
    $d F w x y z $.
    crnghomfo.1 $e |- G = ( 1st ` R ) $.
    crnghomfo.2 $e |- X = ran G $.
    crnghomfo.3 $e |- J = ( 1st ` S ) $.
    crnghomfo.4 $e |- Y = ran J $.
    $( The image of a homomorphism from a commutative ring is commutative. $)
    crnghomfo $p |- ( ( ( R e. CRing /\ S e. Ring ) /\ ( F e. ( R RngHom S )
                                    /\ F : X -onto-> Y ) ) -> S e. CRing ) $=
      ( vy vz vw vx ccring wcel cring cv c2nd cfv co wceq wral wa crnghom wfo
      eqid iscring2 simplr wrex wi foelrn ex anim12d reeanv syl6ibr ad2antll
      w3a opreq12 ancoms eqeq12d crngcom 3expb 3ad2antl1 fveq2d rnghommul
      crngrng syl3anl1 ancom2s 3eqtr3d syl5cbir 3expa adantrr r19.23advv syld
      r19.21aivv sylanbrc ) BPQBRQZLSZMSZBTUAZUBZWAVTWBUBZUCZMGUDLGUDAPQZVSUEZC
      ABUFUBQZFGCUGZUEZUEZLMBEWBGJWBUHZKUIWFVSWJUJWKWELMGGWKVTGQZWAGQZUEZVTNSZC
      UAZUCZWAOSZCUAZUCZUEZOFUKNFUKZWEWIWOXCULWGWHWIWOWRNFUKZXAOFUKZUEXCWIWMXDW
      NXEWIWMXDNFGVTCUMUNWIWNXEOFGWACUMUNUOWRXANOFFUPUQURWKXBWENOFFWGWHWPFQZWSF
      QZUEZXBWEULZULZWIWFVSWHXJWFVSWHUSZXHXIXBWEWQWTWBUBZWTWQWBUBZUCXKXHUEZXBWC
      XLWDXMVTWQWAWTWBUTXAWRWDXMUCWAWTVTWQWBUTVAVBXNWPWSATUAZUBZCUAZWSWPXOUBZCU
      AZXLXMXNXPXRCWFVSXHXPXRUCZWHWFXFXGXTWPWSADXOFHXOUHZIVCVDVEVFARQZVSWHXHXQX
      LUCWFWPWSABCDXOWBFHIYAWLVGAVHZVIYBVSWHXHXSXMUCZWFYBVSWHUSXGXFYDWSWPABCDXO
      WBFHIYAWLVGVJYCVIVKVLUNVMVNVOVPVQVR $.
      $( [25-Jun-2011] $) $( [4-Jan-2011] $)
  $}

