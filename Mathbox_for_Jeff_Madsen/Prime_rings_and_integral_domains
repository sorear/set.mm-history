$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Prime rings and integral domains
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c PrRing $.
  $c Dmn $.

  $( Extend class notation with the class of prime rings. $)
  cprrng $a class PrRing $.

  $( Extend class notation with the class of domains. $)
  cdmn $a class Dmn $.

  $( Define the class of prime rings.  A ring is prime if the zero ideal is a
     prime ideal. $)
  df-prrng $a |- PrRing = { r e. RingOps |
                              { ( Id ` ( 1st ` r ) ) } e. ( PrIdl ` r ) } $.

  $( Define the class of (integral) domains.  A domain is a commutative prime
     ring. $)
  df-dmn $a |- Dmn = ( PrRing i^i Com2 ) $.

  ${
    $d R r $.  $d Z r $.  $d U r $.
    isprrng.1 $e |- G = ( 1st ` R ) $.
    isprrng.2 $e |- Z = ( Id ` G ) $.
    $( The predicate "is a prime ring".  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    isprrng $p |- ( R e. PrRing <-> ( R e. RingOps
                                        /\ { Z } e. ( PrIdl ` R ) ) ) $=
      ( vr cv c1st cfv cgi csn cpridl wcel cring cprrng wceq fveq2 fveq2d sneqd
      syl6eqr eleq12d df-prrng elrab2 ) FGZHIZJIZKZUDLIZMCKZALIZMFANOUDAPZUGUIU
      HUJUKUFCUKUFBJICUKUEBJUKUEAHIBUDAHQDTRETSUDALQUAFUBUC $.
      $( [10-Jun-2010] $)
  $}

  $( A prime ring is a ring.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
  prrngrng $p |- ( R e. PrRing -> R e. RingOps ) $=
    ( cprrng wcel cring c1st cfv cgi csn cpridl eqid isprrng simplbi ) ABCADCAE
    FZGFZHAIFCAMNMJNJKL $.
    $( [10-Jun-2010] $)

  ${
    $d R i j x y $.  $d H x y $.  $d X i j x y $.  $d Z i j x y $.
    $d U i j x y $.
    smprngpr.1 $e |- G = ( 1st ` R ) $.
    smprngpr.2 $e |- H = ( 2nd ` R ) $.
    smprngpr.3 $e |- X = ran G $.
    smprngpr.4 $e |- Z = ( Id ` G ) $.
    smprngpr.5 $e |- U = ( Id ` H ) $.
    $( A simple ring (one whose only ideals are ` 0 ` and ` R ` ) is a prime
       ring.  (Contributed by Jeff Madsen, 6-Jan-2011.) $)
    smprngpr $p |- ( ( R e. RingOps /\ U =/= Z /\ ( Idl ` R ) = { { Z } , X } )
                                                      -> R e. PrRing ) $=
      ( vx vy vj vi wcel wceq wral wi wa wne cidl cfv csn cpr w3a cpridl cprrng
      cring simp1 cv co wss 0idl 3ad2ant1 0ring eqcom 3bitr4g necon3bid 3adant3
      wo biimpa cun df-pr eqeq2i eleq2 anbi12d elun elsn orbi12i anbi12i syl6bb
      bitri sylbi 3ad2ant3 eqimss orcd adantr a1d a1i olcd adantl wrex crn c1st
      wb rneqi eqtri ring1cl ringolidm mpdan eleq1d cgi fvex eqeltri necon3bbid
      wn cvv elsnc biimpar opreq1 notbid opreq2 rcla42ev syl3anc rexnal pm2.21d
      rexbii sylib raleq ralbidv imbi1d syl5ibrcom ccased sylbid ralrimivv 3jca
      sylan9bb ispridl mpbird isprrng sylanbrc ) AUIPZBFUAZAUBUCZFUDZEUEZQZUFZY
      CYFAUGUCPZAUHPYCYDYHUJYIYJYFYEPZYFEUAZLUKZMUKZDULZYFPZMNUKZRZLOUKZRZYSYFU
      MZYQYFUMZVAZSZNYEROYERZUFZYIYKYLUUEYCYDYKYHACFGJUNUOYCYDYLYHYCYDYLYCBFYFE
      YCFBQEYFQBFQZYFEQABCDEFGHIJKUPBFUQYFEUQURUSVBUTYIUUDONYEYEYIYSYEPZYQYEPZT
      ZYSYFQZYSEQZVAZYQYFQZYQEQZVAZTZUUDYHYCUUJUUQWFZYDYHYEYFUDZEUDZVCZQZUURYGU
      VAYEYFEVDVEUVBUUJYSUVAPZYQUVAPZTUUQUVBUUHUVCUUIUVDYEUVAYSVFYEUVAYQVFVGUVC
      UUMUVDUUPUVCYSUUSPZYSUUTPZVAUUMYSUUSUUTVHUVEUUKUVFUULOYFVIOEVIVJVMUVDYQUU
      SPZYQUUTPZVAUUPYQUUSUUTVHUVGUUNUVHUUONYFVINEVIVJVMVKVLVNVOYCYDUUQUUDSYHYC
      YDTZUUKUUNUULUUOUUDUUKUUNTZUUDSUVIUVJUUCYTUUKUUCUUNUUKUUAUUBYSYFVPVQZVRVS
      VTUULUUNTZUUDSUVIUVLUUCYTUUNUUCUULUUNUUBUUAYQYFVPWAWBVSVTUUKUUOTZUUDSUVIU
      VMUUCYTUUKUUCUUOUVKVRVSVTUVIUUDUULUUOTZYPMERZLERZUUCSUVIUVPUUCUVIYPWQZMEW
      CZLEWCZUVPWQZUVIBEPZUWABBDULZYFPZWQZUVSYCUWAYDABDEECWDAWEUCZWDICUWEGWGWHZ
      HKWIZVRZUWHYCUWDYDYCUWCBFYCUWCBYFPUUGYCUWBBYFYCUWAUWBBQUWGBABDEHUWFKWJWKW
      LBFBDWMUCWRKDWMWNWOWSVLWPWTUVQUWDBYNDULZYFPZWQLMBBEEYMBQZYPUWJUWKYOUWIYFY
      MBYNDXAWLXBYNBQZUWJUWCUWLUWIUWBYFYNBBDXCWLXBXDXEUVSUVOWQZLEWCUVTUVRUWMLEY
      PMEXFXHUVOLEXFVMXIXGUVNYTUVPUUCUULYTYRLERUUOUVPYRLYSEXJUUOYRUVOLEYPMYQEXJ
      XKXRXLXMXNUTXOXPXQYCYDYJUUFWFYHLMYFACDEONGHIXSUOXTACFGJYAYB $.
      $( [6-Jan-2011] $)
  $}

  $( A division ring is a prime ring.  (Contributed by Jeff Madsen,
     6-Jan-2011.) $)
  divrngpr $p |- ( R e. DivRingOps -> R e. PrRing ) $=
    ( cdrng wcel cring c2nd cfv cgi c1st wne cidl csn crn wceq cprrng cdif cres
    cpr cxp cgr eqid isdivrng1 simplbi dvrunz divrngidl smprngpr syl3anc ) ABCZ
    ADCZAEFZGFZAHFZGFZIAJFULKZUKLZQMANCUGUHUIUNUMOZUORPSCAUKUIUNULUKTZUITZULTZU
    NTZUAUBAUJUKUIUNULUPUQUSURUJTZUCAUKUIUNULUPUQUSURUDAUJUKUIUNULUPUQUSURUTUEU
    F $.
    $( [6-Jan-2011] $)

  $( The predicate "is a domain".  (Contributed by Jeff Madsen,
     10-Jun-2010.) $)
  isdmn $p |- ( R e. Dmn <-> ( R e. PrRing /\ R e. Com2 ) ) $=
    ( cdmn wcel cprrng ccm2 cin wa df-dmn eleq2i elin bitri ) ABCADEFZCADCAECGB
    LAHIADEJK $.
    $( [10-Jun-2010] $)

  $( The predicate "is a domain".  (Contributed by Jeff Madsen,
     10-Jun-2010.) $)
  isdmn2 $p |- ( R e. Dmn <-> ( R e. PrRing /\ R e. CRing ) ) $=
    ( cdmn wcel cprrng ccm2 wa ccring isdmn cring prrngrng ibar iscring syl6bbr
    wb syl pm5.32i bitri ) ABCADCZAECZFRAGCZFAHRSTRAICZSTNAJUASUASFTUASKALMOPQ
    $.
    $( [10-Jun-2010] $)

  $( A domain is a commutative ring.  (Contributed by Jeff Madsen,
     6-Jan-2011.) $)
  dmncrng $p |- ( R e. Dmn -> R e. CRing ) $=
    ( cdmn wcel cprrng ccring isdmn2 simprbi ) ABCADCAECAFG $.
    $( [6-Jan-2011] $)

  $( A domain is a ring.  (Contributed by Jeff Madsen, 6-Jan-2011.) $)
  dmnrng $p |- ( R e. Dmn -> R e. RingOps ) $=
    ( cdmn wcel ccring cring dmncrng crngrng syl ) ABCADCAECAFAGH $.
    $( [6-Jan-2011] $)

  $( A field is a domain.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
  flddmn $p |- ( K e. Fld -> K e. Dmn ) $=
    ( cdrng wcel ccring cprrng cfld cdmn divrngpr anim12i isfld2 isdmn2 3imtr4i
    wa id ) ABCZADCZMAECZPMAFCAGCOQPPAHPNIAJAKL $.
    $( [10-Jun-2010] $)

