$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Prime rings and integral domains
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c PrRing $.
  $c Dmn $.

  $( Extend class notation with the class of prime rings. $)
  cprrng $a class PrRing $.

  $( Extend class notation with the class of domains. $)
  cdmn $a class Dmn $.

  $( Define the class of prime rings.  A ring is prime if the zero ideal is a
     prime ideal. $)
  df-prrng $a |- PrRing = { r e. Ring |
                              { ( Id ` ( 1st ` r ) ) } e. ( PrIdl ` r ) } $.

  $( Define the class of (integral) domains.  A domain is a commutative prime
     ring. $)
  df-dmn $a |- Dmn = ( PrRing i^i Com2 ) $.

  ${
    $d R r $.  $d Z r $.  $d U r $.
    isprrng.1 $e |- G = ( 1st ` R ) $.
    isprrng.2 $e |- Z = ( Id ` G ) $.
    $( The predicate "is a prime ring". $)
    isprrng $p |- ( R e. PrRing <-> ( R e. Ring
                                        /\ { Z } e. ( PrIdl ` R ) ) ) $=
      ( vr cv c1st cfv cgi csn cpridl wcel cring cprrng wceq fveq2 syl6eqr
      fveq2d sneqd eleq12d df-prrng elrab2 ) FGZHIZJIZKZUDLIZMCKZALIZMFANOUDAPZ
      UGUIUHUJUKUFCUKUFBJICUKUEBJUKUEAHIBUDAHQDRSERTUDALQUAFUBUC $.
      $( [10-Jun-2010] $)
  $}

  $( A prime ring is a ring. $)
  prrngrng $p |- ( R e. PrRing -> R e. Ring ) $=
    ( cprrng wcel cring c1st cfv cgi csn cpridl eqid isprrng simplbi ) ABCADCAE
    FZGFZHAIFCAMNMJNJKL $.
    $( [10-Jun-2010] $)

  ${
    $d R i j x y $.  $d H x y $.  $d X i j x y $.  $d Z i j x y $.
    $d U i j x y $.
    smprngpr.1 $e |- G = ( 1st ` R ) $.
    smprngpr.2 $e |- H = ( 2nd ` R ) $.
    smprngpr.3 $e |- X = ran G $.
    smprngpr.4 $e |- Z = ( Id ` G ) $.
    smprngpr.5 $e |- U = ( Id ` H ) $.
    $( A simple ring (one whose only ideals are ` 0 ` and ` R ` ) is a prime
       ring. $)
    smprngpr $p |- ( ( R e. Ring /\ U =/= Z /\ ( Idl ` R ) = { { Z } , X } )
                                                      -> R e. PrRing ) $=
      ( vx vy vj vi cprrng wcel cring csn cpridl cfv wne cidl cpr wceq w3a
      isprrng simp1 cv co wral wss wo wi 0idl 3ad2ant1 0ring eqcom 3bitr4g
      necon3bid biimpa 3adant3 wa wb cun df-pr eqeq2i eleq2 anbi12d elun elsn
      orbi12i bitri anbi12i syl6bb sylbi 3ad2ant3 eqimss orcd adantr a1d a1i
      olcd adantl raleq ralbidv sylan9bb imbi1d wn wrex crn c1st rneqi eqtri
      ring1cl ringlidm mpdan eleq1d cgi cvv fvex eqeltri elsnc necon3bbid
      biimpar opreq1 notbid opreq2 rcla42ev syl111anc rexnal rexbii sylib
      pm2.21d syl5cbir ccased sylbid r19.21aivv 3jca ispridl mpbird sylanbrc )
      APQARQZFSZATUAQZYCBFUBZAUCUAZYDEUDZUEZUFZACFGJUGYCYFYIUHYJYEYDYGQZYDEUBZL
      UIZMUIZDUJZYDQZMNUIZUKZLOUIZUKZYSYDULZYQYDULZUMZUNZNYGUKOYGUKZUFZYJYKYLUU
      EYCYFYKYIACFGJUOUPYCYFYLYIYCYFYLYCBFYDEYCFBUEEYDUEBFUEZYDEUEABCDEFGHIJKUQ
      BFURYDEURUSUTVAVBYJUUDONYGYGYJYSYGQZYQYGQZVCZYSYDUEZYSEUEZUMZYQYDUEZYQEUE
      ZUMZVCZUUDYIYCUUJUUQVDZYFYIYGYDSZESZVEZUEZUURYHUVAYGYDEVFVGUVBUUJYSUVAQZY
      QUVAQZVCUUQUVBUUHUVCUUIUVDYGUVAYSVHYGUVAYQVHVIUVCUUMUVDUUPUVCYSUUSQZYSUUT
      QZUMUUMYSUUSUUTVJUVEUUKUVFUULOYDVKOEVKVLVMUVDYQUUSQZYQUUTQZUMUUPYQUUSUUTV
      JUVGUUNUVHUUONYDVKNEVKVLVMVNVOVPVQYCYFUUQUUDUNYIYCYFVCZUUKUUNUULUUOUUDUUK
      UUNVCZUUDUNUVIUVJUUCYTUUKUUCUUNUUKUUAUUBYSYDVRVSZVTWAWBUULUUNVCZUUDUNUVIU
      VLUUCYTUUNUUCUULUUNUUBUUAYQYDVRWCWDWAWBUUKUUOVCZUUDUNUVIUVMUUCYTUUKUUCUUO
      UVKVTWAWBUULUUOVCZUUDYPMEUKZLEUKZUUCUNUVIUVNYTUVPUUCUULYTYRLEUKUUOUVPYRLY
      SEWEUUOYRUVOLEYPMYQEWEWFWGWHUVIUVPUUCUVIYPWIZMEWJZLEWJZUVPWIZUVIBEQZUWABB
      DUJZYDQZWIZUVSYCUWAYFABDEECWKAWLUAZWKICUWEGWMWNZHKWOZVTZUWHYCUWDYFYCUWCBF
      YCUWCBYDQUUGYCUWBBYDYCUWAUWBBUEUWGBABDEHUWFKWPWQWRBFBDWSUAWTKDWSXAXBXCVOX
      DXEUVQUWDBYNDUJZYDQZWILMBBEEYMBUEZYPUWJUWKYOUWIYDYMBYNDXFWRXGYNBUEZUWJUWC
      UWLUWIUWBYDYNBBDXHWRXGXIXJUVSUVOWIZLEWJUVTUVRUWMLEYPMEXKXLUVOLEXKVMXMXNXO
      XPVBXQXRXSYCYFYEUUFVDYILMYDACDEONGHIXTUPYAYB $.
      $( [6-Jan-2011] $)
  $}

  $( A division ring is a prime ring. $)
  divrngpr $p |- ( R e. DivRing -> R e. PrRing ) $=
    ( cdrng wcel cring c2nd cfv cgi c1st wne cidl csn crn cpr wceq cprrng cdif
    cxp cres cgr eqid isdivrng1 simplbi dvrunz divrngidl smprngpr syl111anc )
    ABCZADCZAEFZGFZAHFZGFZIAJFULKZUKLZMNAOCUGUHUIUNUMPZUOQRSCAUKUIUNULUKTZUITZU
    LTZUNTZUAUBAUJUKUIUNULUPUQUSURUJTZUCAUKUIUNULUPUQUSURUDAUJUKUIUNULUPUQUSURU
    TUEUF $.
    $( [6-Jan-2011] $)

  $( The predicate "is a domain". $)
  isdmn $p |- ( R e. Dmn <-> ( R e. PrRing /\ R e. Com2 ) ) $=
    ( cdmn wcel cprrng ccm2 cin wa df-dmn eleq2i elin bitri ) ABCADEFZCADCAECGB
    LAHIADEJK $.
    $( [10-Jun-2010] $)

  $( The predicate "is a domain". $)
  isdmn2 $p |- ( R e. Dmn <-> ( R e. PrRing /\ R e. CRing ) ) $=
    ( cdmn wcel cprrng ccm2 wa ccring isdmn cring wb prrngrng ibar iscring
    syl6bbr syl pm5.32i bitri ) ABCADCZAECZFRAGCZFAHRSTRAICZSTJAKUASUASFTUASLAM
    NOPQ $.
    $( [10-Jun-2010] $)

  $( A domain is a commutative ring. $)
  dmncrng $p |- ( R e. Dmn -> R e. CRing ) $=
    ( cdmn wcel cprrng ccring isdmn2 simprbi ) ABCADCAECAFG $.
    $( [6-Jan-2011] $)

  $( A domain is a ring. $)
  dmnrng $p |- ( R e. Dmn -> R e. Ring ) $=
    ( cdmn wcel ccring cring dmncrng crngrng syl ) ABCADCAECAFAGH $.
    $( [6-Jan-2011] $)

  $( A field is a domain. $)
  flddmn $p |- ( K e. Fld -> K e. Dmn ) $=
    ( cdrng wcel ccring wa cprrng cfld cdmn divrngpr id anim12i isfld2 isdmn2
    3imtr4i ) ABCZADCZEAFCZPEAGCAHCOQPPAIPJKALAMN $.
    $( [10-Jun-2010] $)

