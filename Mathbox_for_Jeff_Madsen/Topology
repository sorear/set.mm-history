$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Topology
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The union of two open sets is open.  (Moved to ~ unopn in main set.mm and
     may be deleted by mathbox owner, JM. --NM 15-Oct-2012.) $)
  unopnOLD $p |- ( ( J e. Top /\ A e. J /\ B e. J ) -> ( A u. B ) e. J ) $=
    ( ctop wcel w3a cpr cuni cun wceq uniprg 3adant1 wa wss prssg ibi uniopn
    sylan2 3impb eqeltrrd ) CDEZACEZBCEZFABGZHZABIZCUBUCUEUFJUAABCCKLUAUBUCUECE
    ZUBUCMZUAUDCNZUGUHUIABCCCOPUDCQRST $.
    $( [2-Sep-2009] $)

  ${
    $d A a $.  $d B a $.
    $( The intersection of two closed sets is closed.  (Moved to ~ incld in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       15-Oct-2012.) $)
    incldOLD $p |- ( ( J e. Top /\ A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) )
                                      -> ( A i^i B ) e. ( Clsd ` J ) ) $=
      ( va ctop wcel ccld cfv w3a cpr cint cin wceq intprg 3adant1 c0 wne wss
      simp1 cv preq1 neeq1d visset prnz vtoclg 3ad2ant2 wa prssg ibi intcld
      syl111anc eqeltrrd ) CEFZACGHZFZBUNFZIZABJZKZABLZUNUOUPUSUTMUMABUNUNNOUQU
      MURPQZURUNRZUSUNFUMUOUPSUOUMVAUPDTZBJZPQVADAUNVCAMVDURPVCABUAUBVCBDUCUDUE
      UFUOUPVBUMUOUPUGVBABUNUNUNUHUIOURCUJUKUL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d X v $.  $d A v $.  $d B v $.
    subspopn.1 $e |- X = U. J $.
    $( An open set is open in the subspace topology. $)
    subspopn $p |- ( ( ( J e. Top /\ A C_ X ) /\ ( B e. J /\ B C_ A ) )
                                      -> B e. ( subSp ` <. A , J >. ) ) $=
      ( vv ctop wcel wss wa cop csubsp cfv cv cin wceq wrex simprl dfss biimpi
      ad2antll ineq1 eqeq2d rcla4ev syl11anc cvv wb simpll cuni uniexg
      syl5eqel ssexg sylan2 ancoms adantr issubspt syl111anc mpbird ) CGHZADIZJ
      ZBCHZBAIZJZJZBACKLMHZBFNZAOZPZFCQZVEVBBBAOZPZVJVAVBVCRZVCVLVAVBVCVLBASTUA
      VIVLFBCVGBPVHVKBVGBAUBUCUDUEVEUSVBAUFHZVFVJUGUSUTVDUHVMVAVNVDUTUSVNUSUTDU
      FHVNUSCUIUFDCGUJEUKADUFULUMUNUOFBACCUPUQUR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d X v $.  $d A v $.  $d B v $.
    subspcld.1 $e |- X = U. J $.
    $( A closed set is closed in the subspace topology. $)
    subspcld $p |- ( ( ( J e. Top /\ A C_ X ) /\ ( B e. ( Clsd ` J )
                /\ B C_ A ) ) -> B e. ( Clsd ` ( subSp ` <. A , J >. ) ) ) $=
      ( vv ctop wcel wss wa ccld cfv cop csubsp cv cin wceq wrex simprl dfss
      biimpi ad2antll ineq1 eqeq2d rcla4ev syl11anc wb subcld adantr mpbird )
      CGHADIJZBCKLZHZBAIZJZJZBACMNLKLHZBFOZAPZQZFULRZUPUMBBAPZQZVAUKUMUNSUNVCUK
      UMUNVCBATUAUBUTVCFBULURBQUSVBBURBAUCUDUEUFUKUQVAUGUOFBACDEUHUIUJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A v $.  $d B v $.  $d J v $.
    $( A set which is closed in the subspace topology induced by a closed set
       is closed in the original topology. $)
    subspcld2 $p |- ( ( J e. Top /\ A e. ( Clsd ` J ) /\
            B e. ( Clsd ` ( subSp ` <. A , J >. ) ) ) -> B e. ( Clsd ` J ) ) $=
      ( vv ctop wcel ccld cfv cop csubsp wa cv cin wceq wrex cuni wss wb eqid
      cldss subcld syldan eleq1 incld 3expa an32s syl5cbir r19.23adva sylbid
      3impia ) CEFZACGHZFZBACIJHGHFZBULFZUKUMKZUNBDLZAMZNZDULOZUOUKUMACPZQUNUTR
      ACVAVASZTDBACVAVBUAUBUPUSUODULUSUOURULFZUPUQULFZKBURULUCUKVDUMVCUKVDUMVCU
      QACUDUEUFUGUHUIUJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J w x y z $.  $d X w x y z $.  $d A w x y z $.  $d B w x y z $.
    subspabs.1 $e |- X = U. J $.
    $( Absorption law for subspace. $)
    subspabs $p |- ( ( J e. Top /\ A C_ X /\ B C_ A ) -> ( subSp ` <. B ,
                  ( subSp ` <. A , J >. ) >. ) = ( subSp ` <. B , J >. ) ) $=
      ( vx vy vz vw ctop wcel wss w3a cop csubsp cfv cv cin wceq wrex wi cvv
      wb simp1 visset a1i cuni uniexg syl5eqel ssexg sylan2 ancoms 3adant3
      issubspt syl111anc wa simpllr ineq1 adantl sseqin2 biimpi 3ad2ant3
      ineq2d inass syl5eq ad2antrr eqtrd eqeq2d biimpa rcla4ev syl11anc exp31
      r19.23adva sylbid r19.23adv eqid mpan2 simpll inex1 adantr mpbird
      3adantl3 eqcomd syl6eqr ex impbid sseq2i stoig3 sylan2b sstr 3impb
      3bitr4d eqrdv ) CJKZADLZBALZMZFBACNOPZNOPZBCNOPZWQFQZGQZBRZSZGWRTZXAHQZBR
      ZSZHCTZXAWSKZXAWTKZWQXEXIWQXDXIGWRWQXBWRKZXBIQZARZSZICTZXDXIUAZWQWNXBUBKZ
      AUBKZXLXPUCWNWOWPUDZXRWQGUEUFWNWOXSWPWOWNXSWNWODUBKZXSWNCUGZUBDCJUHEUIZAD
      UBUJUKULZUMIXBAUBCUNUOWQXOXQICWQXMCKZUPZXOXDXIYFXOUPZXDUPYEXAXMBRZSZXIWQY
      EXOXDUQYGXDYIYGXCYHXAYGXCXNBRZYHXOXCYJSYFXBXNBURUSWQYJYHSYEXOWQXMABRZRYHY
      JWQYKBXMWPWNYKBSZWOWPYLBAUTVAZVBVCXMABVDVEVFVGVHVIXHYIHXMCXFXMSXGYHXAXFXM
      BURVHVJVKVLVMVNVOWQXHXEHCWQXFCKZUPZXHXEYOXHUPXFARZWRKZXAYPBRZSZXEYOYQXHWN
      WOYNYQWPWNWOUPZYNUPZYQYPXBARZSZGCTZYNUUDYTYNYPYPSZUUDYPVPUUCUUEGXFCXBXFSU
      UBYPYPXBXFAURVHVJVQUSUUAWNYPUBKZXSYQUUDUCWNWOYNVRUUFUUAXFAHUEVSUFYTXSYNYD
      VTGYPAUBCUNUOWAWBVTYOXHYSYOXGYRXAYOXGXFYKRYRYOBYKXFWQBYKSZYNWPWNUUGWOWPYK
      BYMWCVBVTVCXFABVDWDVHVIXDYSGYPWRXBYPSXCYRXAXBYPBURVHVJVKWEVMWFWQWRJKZXAUB
      KZBUBKZXJXEUCWNWOUUHWPWOWNAYBLUUHDYBAEWGACWHWIUMUUIWQFUEUFZWNWOWPUUJWOWPU
      PWNBDLZUUJWPWOUULBADWJULUULWNUUJWNUULYAUUJYCBDUBUJUKULUKWKZGXABUBWRUNUOWQ
      WNUUIUUJXKXIUCXTUUKUUMHXABUBCUNUOWLWM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J x y z $.  $d X x y z $.  $d N x y z $.  $d S x y z $.
    neificl.1 $e |- X = U. J $.
    $( Neighborhoods are closed under finite intersection. $)
    neificl $p |- ( ( ( J e. Top /\ N C_ ( ( nei ` J ) ` S ) ) /\
              ( N e. Fin /\ N =/= (/) ) ) -> |^| N e. ( ( nei ` J ) ` S ) ) $=
      ( vz vx vy ctop wcel cnei cfv wss cfn c0 wne wa cint cfi cv wceq w3a wex
      eqid wi sseq1 eleq1 inteq eqeq2d 3anbi123d cla4egv 3ad2ant2 pm2.43i
      mp3an3 adantrr wb cvv intex fvex sppfi mpan2 sylbi ad2antll mpbird cin
      wral innei 3expib r19.21aivv inficl sylancr eleq2d biimpa sylan2 anassrs
      ) BIJZCABKLZLZMZCNJZCOPZQZCRZVRJZVSWBQZVPWCVRSLZJZWDWEWGFTZVRMZWHNJZWCWHR
      ZUAZUBZFUCZVSVTWNWAVSVTWCWCUAZWNWCUDVSVTWOUBZWNVTVSWPWNUEWOWMWPFCNWHCUAZW
      IVSWJVTWLWOWHCVRUFWHCNUGWQWKWCWCWHCUHUIUJUKULUMUNUOWAWGWNUPZVSVTWAWCUQJZW
      RCURWSVRUQJZWRAVQUSZFWCUQVRUQUTVAVBVCVDVPWGWDVPWFVRWCVPWTGTZHTZVEVRJZHVRV
      FGVRVFWFVRUAXAVPXDGHVRVRVPXBVRJXCVRJXDABXCXBVGVHVIGHVRUQVJVKVLVMVNVO $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J x $.  $d X x $.  $d A x $.  $d B x $.
    lpss2.1 $e |- X = U. J $.
    $( Limit points of a subset are limit points of the larger set. $)
    lpss2 $p |- ( ( J e. Top /\ A C_ X /\ B C_ A ) ->
                          ( ( limPt ` J ) ` B ) C_ ( ( limPt ` J ) ` A ) ) $=
      ( vx ctop wcel wss w3a clp cfv cv csn cdif ccl clsss id ssdifss ssdif
      syl3an sseld wb wa sstr ancoms islp sylan2 3impb 3adant3 3imtr4d ssrdv )
      CGHZADIZBAIZJZFBCKLZLZAUQLZUPFMZBUTNZOZCPLZLZHZUTAVAOZVCLZHZUTURHZUTUSHZU
      PVDVGUTUMVFDIVBVFIVDVGIUMUNUOVFVBCDEQUMRADVASBAVATUAUBUMUNUOVIVEUCZUNUOUD
      UMBDIZVKUOUNVLBADUEUFUTBCDEUGUHUIUMUNVJVHUCUOUTACDEUGUJUKUL $.
      $( [2-Sep-2009] $)
  $}

