$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Topology
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The union of two open sets is open.  (Moved to ~ unopn in main set.mm and
     may be deleted by mathbox owner, JM. --NM 15-Oct-2012.) $)
  unopnOLD $p |- ( ( J e. Top /\ A e. J /\ B e. J ) -> ( A u. B ) e. J ) $=
    ( unopn ) ABCD $.
    $( [2-Sep-2009] $)

  ${
    $d A a $.  $d B a $.
    $( The intersection of two closed sets is closed.  (Moved to ~ incld in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       15-Oct-2012.) $)
    incldOLD $p |- ( ( J e. Top /\ A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) )
                                      -> ( A i^i B ) e. ( Clsd ` J ) ) $=
      ( incld ) ABCD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d X v $.  $d A v $.  $d B v $.
    $( An open set is open in the subspace topology.  (Contributed by Jeff
       Madsen, 2-Sep-2009.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    subspopn $p |- ( ( ( J e. Top /\ A e. V ) /\ ( B e. J /\ B C_ A ) )
                                      -> B e. ( subSp ` <. A , J >. ) ) $=
      ( ctop wcel wa wss cop csubsp cfv wi w3a cin elsubspr wceq wb df-ss
      eleq1 sylbi syl5ibcom 3expa impr ) CEFZADFZGBCFZBAHZBACIJKZFZUDUEUFUGUILU
      DUEUFMBANZUHFZUGUIBACDOUGUJBPUKUIQBARUJBUHSTUAUBUC $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d X v $.  $d A v $.  $d B v $.
    subspcld.1 $e |- X = U. J $.
    $( A closed set is closed in the subspace topology.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    subspcld $p |- ( ( ( J e. Top /\ A C_ X ) /\ ( B e. ( Clsd ` J )
                /\ B C_ A ) ) -> B e. ( Clsd ` ( subSp ` <. A , J >. ) ) ) $=
      ( vv ctop wcel wss wa ccld cfv cop csubsp cv cin wceq wrex simprl dfss
      biimpi ad2antll ineq1 eqeq2d rcla4ev syl11anc wb subcld adantr mpbird )
      CGHADIJZBCKLZHZBAIZJZJZBACMNLKLHZBFOZAPZQZFULRZUPUMBBAPZQZVAUKUMUNSUNVCUK
      UMUNVCBATUAUBUTVCFBULURBQUSVBBURBAUCUDUEUFUKUQVAUGUOFBACDEUHUIUJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A v $.  $d B v $.  $d J v $.
    $( A set which is closed in the subspace topology induced by a closed set
       is closed in the original topology.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    subspcld2 $p |- ( ( J e. Top /\ A e. ( Clsd ` J ) /\
            B e. ( Clsd ` ( subSp ` <. A , J >. ) ) ) -> B e. ( Clsd ` J ) ) $=
      ( vv ctop wcel ccld cfv cop csubsp wa cv cin wceq wrex cuni wss wb eqid
      cldss subcld syldan incld 3expa an32s eleq1 syl5ibrcom r19.23adva sylbid
      3impia ) CEFZACGHZFZBACIJHGHFZBULFZUKUMKZUNBDLZAMZNZDULOZUOUKUMACPZQUNUTR
      ACVAVASZTDBACVAVBUAUBUPUSUODULUPUQULFZKUOUSURULFZUKVCUMVDUKVCUMVDUQACUCUD
      UEBURULUFUGUHUIUJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J x y $.  $d N x $.  $d S x y $.
    $( Neighborhoods are closed under finite intersection.  (Contributed by
       Jeff Madsen, 2-Sep-2009.)  (Revised by Mario Carneiro, 25-Nov-2013.) $)
    neificl $p |- ( ( ( J e. Top /\ N C_ ( ( nei ` J ) ` S ) ) /\
              ( N e. Fin /\ N =/= (/) ) ) -> |^| N e. ( ( nei ` J ) ` S ) ) $=
      ( vx vy ctop wcel cnei cfv wss cfn c0 wne wa cint simprl cv w3a wi wal
      cin wral innei 3expib r19.21aivv fiint sylib wceq sseq1 neeq1 eleq1
      3anbi123d 3ancomb 3anass bitri syl6bb inteq eleq1d imbi12d cla4gv syl5
      com3l mpdi imp anassrs ) BFGZCABHIIZJZCKGZCLMZNZCOZVGGZVFVHVKNZVMVFVNVIVM
      VHVIVJPVIVFVNVMVFDQZVGJZVOLMZVOKGZRZVOOZVGGZSZDTZVIVNVMSZVFVOEQZUAVGGZEVG
      UBDVGUBWCVFWFDEVGVGVFVOVGGWEVGGWFABWEVOUCUDUEDEVGUFUGWBWDDCKVOCUHZVSVNWAV
      MWGVSVHVJVIRZVNWGVPVHVQVJVRVIVOCVGUIVOCLUJVOCKUKULWHVHVIVJRVNVHVJVIUMVHVI
      VJUNUOUPWGVTVLVGVOCUQURUSUTVAVBVCVDVE $.
      $( [25-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J x $.  $d X x $.  $d A x $.  $d B x $.
    lpss2.1 $e |- X = U. J $.
    $( Limit points of a subset are limit points of the larger set.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    lpss2 $p |- ( ( J e. Top /\ A C_ X /\ B C_ A ) ->
                          ( ( limPt ` J ) ` B ) C_ ( ( limPt ` J ) ` A ) ) $=
      ( vx ctop wcel wss w3a clp cfv cv csn cdif ccl id ssdifss ssdif clsss
      syl3an sseld wb wa sstr ancoms islp sylan2 3impb 3adant3 3imtr4d ssrdv )
      CGHZADIZBAIZJZFBCKLZLZAUQLZUPFMZBUTNZOZCPLZLZHZUTAVAOZVCLZHZUTURHZUTUSHZU
      PVDVGUTUMUMUNVFDIUOVBVFIVDVGIUMQADVARBAVASVFVBCDETUAUBUMUNUOVIVEUCZUNUOUD
      UMBDIZVKUOUNVLBADUEUFUTBCDEUGUHUIUMUNVJVHUCUOUTACDEUGUJUKUL $.
      $( [2-Sep-2009] $)
  $}

