$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Groups and related structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    exidcl.1 $e |- X = ran G $.
    $( Closure of the binary operation of a magma with identity. $)
    exidcl $p |- ( ( G e. ( Magma i^i ExId ) /\ A e. X /\ B e. X )
                                            -> ( A G B ) e. X ) $=
      ( cmagm cexid cin wcel w3a co cdm wa crn rngopid syl5eq eleq2d anbi12d
      pm5.32i eqid clmgm inss1 sseli syl3an1 3expb sylbi 3impb wceq 3ad2ant1
      eleqtrrd ) CFGHZIZADIZBDIZJABCKZCLLZDULUMUNUOUPIZULUMUNMZMULAUPIZBUPIZMZM
      UQULURVAULUMUSUNUTULDUPAULCNUPDCOEPZQULDUPBVBQRSULUSUTUQCFIUSUTUQULABCUPU
      PTUAUKFCFGUBUCUDUEUFUGULUMDUPUHUNVBUIUJ $.
      $( [25-Jun-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d G x $.  $d Y x $.  $d X x $.  $d U x u $.  $d H x u $.
    exidres.1 $e |- X = ran G $.
    exidres.2 $e |- U = ( Id ` G ) $.
    exidres.3 $e |- H = ( G |` ( Y X. Y ) ) $.
    $( Lemma for ~ exidres and ~ exidresid . $)
    exidreslem $p |- ( ( G e. ( Magma i^i ExId ) /\ Y C_ X /\ U e. Y ) ->
                              ( U e. dom dom H /\ A. x e. dom dom H
                            ( ( x H U ) = x /\ ( U H x ) = x ) ) ) $=
      ( cmagm cexid cin wcel wss w3a cdm cv co wceq wa wral cxp cres wfo wf
      opidon2 fof fdm 3syl sseq2d xpss12 anidms syl5bir imp ssdmres sylib
      dmeqi syl5eq dmeqd dmxpid syl6eq eleq2d biimp3ar cmpidelt ssel2 sylan2
      anassrs adantrl ancomd wb oprvres opreqi ancoms eqeq1d anbi12d adantl
      mpbird r19.21aiva 3impa 3adant3 raleq1d jca ) CJKLMZFENZBFMZOZBDPZPZMZAQZ
      BDRZWJSZBWJDRZWJSZTZAWHUAZWCWDWIWEWCWDTZWHFBWQWHFFUBZPZFWQWGWRWQCWRUCZPZW
      RWGWQWRCPZNZXAWRSZWCWDXCWCXCWREEUBZNZWDWCXBXEWRWCXEECUDXEECUEXBXESCEGUFXE
      ECUGXEECUHUIUJWDXFFEFEUKULUMUNZWRCUOZUPDWTIUQZURUSFUTZVAVBVCWFWPWOAFUAZWC
      WDWEXKWQWETWOAFWQWEWJFMZWOWQWEXLTZTZWOWJBCRZWJSZBWJCRZWJSZTZXNXRXPWQXLXRX
      PTZWEWCWDXLXTWCWJEMXTWDXLTWJBCEGHVDFEWJVEVFVGVHVIXMWOXSVJWQXMWLXPWNXRXMWK
      XOWJXLWEWKXOSXLWETWJBWTRXOWKWJBFFCVKDWTWJBIVLURVMVNXMWMXQWJXMBWJWTRXQWMBW
      JFFCVKDWTBWJIVLURVNVOVPVQVGVRVSWFWOAWHFWFWHWSFWFWGWRWFXAWRWGWFXCXDWCWDXCW
      EXGVTXHUPXIURUSXJVAWAVQWB $.
      $( [25-Jun-2011] $) $( [8-Jun-2010] $)

    $( The restriction of a binary operation with identity to a subset
       containing the identity has an identity element. $)
    exidres $p |- ( ( G e. ( Magma i^i ExId ) /\ Y C_ X /\ U e. Y )
                                                      -> H e. ExId ) $=
      ( vx vu cmagm cexid cin wcel wss w3a cv co wceq wa cdm wral wrex
      exidreslem opreq2 eqeq1d opreq1 anbi12d ralbidv rcla4ev syl wb cvv cxp
      cres resexg syl5eqel eqid isexid 3ad2ant1 mpbird ) BKLMZNZEDOZAENZPZCLNZI
      QZJQZCRZVHSZVIVHCRZVHSZTZICUAUAZUBZJVOUCZVFAVONVHACRZVHSZAVHCRZVHSZTZIVOU
      BZTVQIABCDEFGHUDVPWCJAVOVIASZVNWBIVOWDVKVSVMWAWDVJVRVHVIAVHCUEUFWDVLVTVHV
      IAVHCUGUFUHUIUJUKVCVDVGVQULZVEVCCUMNWEVCBEEUNZUOUMCBWFVBUPHUQJIUMCVOVOURU
      SUKUTVA $.
      $( [25-Jun-2011] $) $( [8-Jun-2010] $)

    $( The restriction of a binary operation with identity to a subset
       containing the identity has the same identity element. $)
    exidresid $p |- ( ( ( G e. ( Magma i^i ExId ) /\ Y C_ X /\ U e. Y ) /\
                                        H e. Magma ) -> ( Id ` H ) = U ) $=
      ( vu vx cmagm cexid cin wcel wss w3a wa cgi cfv cv co wceq crn wral crab
      cuni cvv cxp cres resexg syl5eqel eqid idrval syl 3ad2ant1 adantr cdm
      exidreslem pm3.27d elin rngopid sylbir ancoms exidres sylan wb ancom a1i
      raleqbidv mpbird wreu opreq1 eqeq1d opreq2 anbi12d ralbidv reuuni2
      pm3.26d eleqtrrd exidu1 sylanc mpbid eqtrd ) BKLMZNZEDOZAENZPZCKNZQZCRSZI
      TZJTZCUAZWMUBZWMWLCUAZWMUBZQZJCUCZUDZIWSUEUFZAWHWKXAUBZWIWEWFXBWGWECUGNXB
      WEBEEUHZUIUGCBXCWDUJHUKJIUGWKCWSWSULZWKULUMUNUOUPWJAWMCUAZWMUBZWMACUAZWMU
      BZQZJWSUDZXAAUBZWJXJXHXFQZJCUQUQZUDZWHXNWIWHAXMNZXNJABCDEFGHURZUSUPWJXIXL
      JWSXMCLNZWIWSXMUBZWHWIXQXRWIXQQZCWDNZXRCKLUTZCVAVBVCABCDEFGHVDZVEZXIXLVFW
      JXFXHVGVHVIVJAWSNWTIWSVKZXJXKVFWJWTXJIWSAWLAUBZWRXIJWSYEWOXFWQXHYEWNXEWMW
      LAWMCVLVMYEWPXGWMWLAWMCVNVMVOVPVQWJAXMWSWHXOWIWHXOXNXPVRUPYCVSXQWIYDWHWIX
      QYDXSXTYDYAJICWSXDVTVBVCYBVEWAWBWC $.
      $( [25-Jun-2011] $) $( [8-Jun-2010] $)
  $}

  ${
    $d ph u x y z $.  $d G u n x y z $.  $d X u n x y z $.  $d U u n x y z $.
    $d N n $.
    isgrpda.1 $e |- ( ph -> X e. _V ) $.
    isgrpda.2 $e |- ( ph -> G : ( X X. X ) --> X ) $.
    isgrpda.3 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                            -> ( ( x G y ) G z ) = ( x G ( y G z ) ) ) $.
    isgrpda.4 $e |- ( ph -> U e. X ) $.
    isgrpda.5 $e |- ( ( ph /\ x e. X ) -> ( U G x ) = x ) $.

    ${
      isgrpda.6 $e |- ( ( ph /\ x e. X ) -> E. n e. X ( n G x ) = U ) $.
      $( Properties that determine a group operation. $)
      isgrpda $p |- ( ph -> G e. Grp ) $=
        ( vu cgr wcel crn cxp wf cv co wceq wral wrex wa w3a wi 3exp2 imp3a
        r19.21adv r19.21aivv opreq1 eqeq1d eqeq2 rexbidv anbi12d ralbidv
        rcla4ev cbvrexv sylibr jca r19.21aiva sylanc 3jca wb wfo fooprv
        rcla4eopr adantr pm3.27 eqcomd syl3anc sylanbrc forn xpeq1 3syl xpeq2
        eqtrd feq2 syl feq3 bitrd raleq1d raleqbidv rexeq1d anbi2d rexeqbidv
        3anbi123d mpbird cvv fex xpexg eqid isgrp ) AGPQZGRZWQSZWQGTZBUAZCUAZGU
        BDUAZGUBWTXAXBGUBZGUBUCZDWQUDZCWQUDZBWQUDZOUAZWTGUBZWTUCZXAWTGUBZXHUCZC
        WQUEZUFZBWQUDZOWQUEZUGZAXQHHSZHGTZXDDHUDZCHUDZBHUDZXJXLCHUEZUFZBHUDZOHU
        EZUGAXSYBYFJAXTBCHHAWTHQZXAHQZUFXDDHAYGYHXBHQZXDUHAYGYHYIXDKUIUJUKULEHQ
        ZEWTGUBZWTUCZXKEUCZCHUEZUFZBHUDZYFAYEYPOEHXHEUCZYDYOBHYQXJYLYCYNYQXIYKW
        TXHEWTGUMUNYQXLYMCHXHEXKUOUPUQURUSLAYOBHAYGUFZYLYNMYRFUAZWTGUBZEUCZFHUE
        YNNYMUUACFHXAYSUCXKYTEXAYSWTGUMUNUTVAVBVCVDVEAWSXSXGYBXPYFAWSXRWQGTZXSA
        WRXRUCWSUUBVFAWRHWQSZXRAXRHGVGZWQHUCZWRUUCUCUUDXSWTXCUCDHUECHUEZBHUDACD
        BHHHGVHJAUUFBHYJYGWTYKUCUUFYRCDHHEWTWTGVIAYJYGLVJAYGVKYRYKWTMVLVMVCVNZX
        RHGVOZWQHWQVPVQAUUDUUEUUCXRUCUUGUUHWQHHVRVQVSWRXRWQGVTWAAUUDUUEUUBXSVFU
        UGUUHWQHXRGWBVQWCAXFYABWQHAUUDUUEUUGUUHWAZAXEXTCWQHUUIAXDDWQHUUIWDWEWEA
        XOYEOWQHUUIAXNYDBWQHUUIAXMYCXJAXLCWQHUUIWFWGWEWHWIWJAGWKQZWPXQVFXSXRWKQ
        ZUUJAXRHWKGWLJHWKQZUULUUKAHHWKWKWMIIVDVDBCDOWKGWQWQWNWOWAWJ $.
        $( [25-Jun-2011] $) $( [1-Dec-2009] $)
    $}

    ${
      isgrpd.6 $e |- ( ( ph /\ x e. X ) -> N e. X ) $.
      isgrpd.7 $e |- ( ( ph /\ x e. X ) -> ( N G x ) = U ) $.
      $( Properties that determine a group operation. $)
      isgrpd $p |- ( ph -> G e. Grp ) $=
        ( vn wcel cv co wceq wrex wa opreq1 eqeq1d rcla4ev sylanc isgrpda ) ABC
        DEPFHIJKLMGHQGBRZFSZETZPRZUHFSZETZPHUAAUHHQUBUMUJPGHUKGTULUIEUKGUHFUCUD
        UENOUFUG $.
        $( [25-Jun-2011] $) $( [1-Dec-2009] $)
    $}

    ${
      isablda.6 $e |- ( ( ph /\ x e. X ) -> E. n e. X ( n G x ) = U ) $.
      isablda.7 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                              -> ( x G y ) = ( y G x ) ) $.
      $( Properties that determine an Abelian group operation. $)
      isablda $p |- ( ph -> G e. Abel ) $=
        ( cabl wcel cgr cv co wceq crn wral eqid isabl isgrpda wa cdm grprndm
        syl cxp wf fdm dmeqd dmxpid syl6eq eqtrd eleq2d anbi12d ex sylbid
        r19.21aivv sylanbrc ) GPQGRQZBSZCSZGTVFVEGTUAZCGUBZUCBVHUCABCGVHVHUDUEA
        BCDEFGHIJKLMNUFZAVGBCVHVHAVEVHQZVFVHQZUGVEHQZVFHQZUGZVGAVJVLVKVMAVHHVEA
        VHGUHZUHZHAVDVHVPUAVIGUIUJAVPHHUKZUHHAVOVQAVQHGULVOVQUAJVQHGUMUJUNHUOUP
        UQZURAVHHVFVRURUSAVNVGOUTVAVBVC $.
        $( [25-Jun-2011] $) $( [11-Jun-2010] $)
    $}

    ${
      isabld.6 $e |- ( ( ph /\ x e. X ) -> N e. X ) $.
      isabld.7 $e |- ( ( ph /\ x e. X ) -> ( N G x ) = U ) $.
      isabld.8 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                                            -> ( x G y ) = ( y G x ) ) $.
      $( Properties that determine an Abelian group operation. $)
      isabld $p |- ( ph -> G e. Abel ) $=
        ( vn wcel cv co wceq wrex wa opreq1 eqeq1d rcla4ev sylanc isablda ) ABC
        DEQFHIJKLMGHRGBSZFTZEUAZQSZUIFTZEUAZQHUBAUIHRUCUNUKQGHULGUAUMUJEULGUIFU
        DUEUFNOUGPUH $.
        $( [25-Jun-2011] $) $( [5-Dec-2009] $)
    $}
  $}

  ${
    abl4pnp.1 $e |- X = ran G $.
    abl4pnp.2 $e |- D = ( /g ` G ) $.
    $( A commutative/associative law for Abelian groups. $)
    abl4pnp $p |- ( ( G e. Abel /\ ( ( A e. X /\ B e. X ) /\
                  ( C e. X /\ F e. X ) ) ) -> ( ( A G B ) D ( C G F ) ) =
                                            ( ( A D C ) G ( B D F ) ) ) $=
      ( cabl wcel wa co wceq w3a ablmuldiv df-3an sylan2br adantrrr opreq1d
      cgr wi ablgrp grpcl 3expib syl anim1d 3anass syl6ibr imp abldivdiv4
      syldan grpdivcl an4 3imtr4g grpmuldivass sylan 3eqtr3d ) FJKZAGKZBGKZLZCG
      KZEGKZLZLZLZABFMZCDMZEDMZACDMZBFMZEDMZVHCEFMDMZVKBEDMFMZVGVIVLEDUSVBVCVIV
      LNZVDUSUTVAVCOVPVBVCLABCDFGHIPUTVAVCQRSTUSVFVHGKZVCVDOZVJVNNUSVFVRUSVFVQV
      ELVRUSVBVQVEUSFUAKZVBVQUBFUCZVSUTVAVQABFGHUDUEUFUGVQVCVDUHUIUJVHCEDFGHIUK
      ULVSVFVMVONZUSVSVFVKGKZVAVDOZWAVSVFWCVSUTVCLZVAVDLZLWBWELVFWCVSWDWBWEVSUT
      VCWBACDFGHIUMUEUGUTVAVCVDUNWBVAVDUHUOUJVKBEDFGHIUPULVTUQUR $.
      $( [25-Jun-2011] $) $( [11-Jun-2010] $)
  $}

  ${
    grpeqdivid.1 $e |- X = ran G $.
    grpeqdivid.2 $e |- U = ( Id ` G ) $.
    grpeqdivid.3 $e |- D = ( /g ` G ) $.
    $( Two group elements are equal iff their quotient is the identity. $)
    grpeqdivid $p |- ( ( G e. Grp /\ A e. X /\ B e. X ) ->
                                              ( A = B <-> ( A D B ) = U ) ) $=
      ( cgr wcel w3a wceq co opreq1 eqeq1d grpdivid 3adant2 syl5cbir grpnpcan
      grplid eqeq12d syl5bi impbid ) EJKZAFKZBFKZLZABMZABCNZDMZUIUKBBCNZDMZUHUI
      UJULDABBCOPUEUGUMUFBCDEFGIHQRSUHUJBENZDBENZMUIUKUHUNAUOBABCEFGITUEUGUOBMU
      FBDEFGHUARUBUJDBEOUCUD $.
      $( [25-Jun-2011] $) $( [6-Jan-2011] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.  $d X x y $.
    ghomf.1 $e |- X = ran G $.
    ghomf.2 $e |- W = ran H $.
    $( Mapping property of a group homomorphism. $)
    ghomf $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) )
                                                  -> F : X --> W ) $=
      ( vx vy cgr wcel cghom co wf wa cv cfv wceq wral elghom pm3.26bda 3impa
      ) BJKZCJKZABCLMKZEDANZUCUDOUEUFHPZAQIPZAQCMUGUHBMAQRIESHESHIABCDEFGTUAUB
      $.
      $( [25-Jun-2011] $) $( [1-Dec-2009] $)
  $}

  ${
    $d S u v x y $.  $d T u v x y $.  $d G u v x y $.  $d H u v x y $.
    $d K u v x y $.
    $( The composition of two group homomorphisms is a group homomorphism. $)
    ghomco $p |- ( ( ( G e. Grp /\ H e. Grp /\ K e. Grp ) /\
                    ( S e. ( G GrpHom H ) /\ T e. ( H GrpHom K ) ) ) ->
                                      ( T o. S ) e. ( G GrpHom K ) ) $=
      ( vx vy vu vv cgr wcel w3a cghom co wa ccom crn wf cv cfv wceq wral wi
      fco ancoms ad2ant2r a1i fveq2 opreq1d opreq1 fveq2d eqeq12d opreq2d
      opreq2 rcla42v imp ffvelrn adantrr adantrl jca sylan an1rs adantllr
      sylan9eq anasss wfun cdm fvco ffun ad2antlr ad2antrr fdm eleq2d biimpar
      adantlr syl3anc opreq12d eqid grpcl 3expb sylan2 anassrs 3eqtr4d expr
      r19.20dva ex com23 com12 3ad2ant1 jcad wb elghom 3adant3 3adant1 anbi12d
      3adant2 3imtr4d ) CJKZDJKZEJKZLZACDMNKZBDEMNKZOZBAPZCEMNKZXACQZDQZARZFSZA
      TZGSZATZDNZXJXLCNZATZUAZGXGUBZFXGUBZOZXHEQZBRZHSZBTZISZBTZENZYCYEDNZBTZUA
      ZIXHUBHXHUBZOZOZXGYAXERZXJXETZXLXETZENZXOXETZUAZGXGUBZFXGUBZOZXDXFXAYMYNU
      UAYMYNUCXAXIYBYNXSYKYBXIYNXGXHYABAUDUEUFUGWRWSYMUUAUCWTYMWRUUAXIYLXSWRUUA
      UCZXIYLOXSUUCXIYBYKXSUUCUCXIYBOZYKOZWRXSUUAUUEWRXSUUAUCZUUDWRYKUUFUUDWROZ
      YKOZXRYTFXGUUHXJXGKZOXQYSGXGUUHUUIXLXGKZXQYSUCUUHUUIUUJOZXQYSUUHUUKXQOOXK
      BTZXMBTZENZXPBTZYQYRUUHUUKXQUUNUUOUAUUHUUKOXQUUNXNBTZUUOUUDYKUUKUUNUUPUAZ
      WRXIYKUUKUUQYBXIUUKYKUUQXKXHKZXMXHKZOZYKUUQXIUUKOZUUTYKUUQYJUUQUULYFENZXK
      YEDNZBTZUAHIXKXMXHXHYCXKUAZYGUVBYIUVDUVEYDUULYFEYCXKBUHUIUVEYHUVCBYCXKYED
      UJUKULYEXMUAZUVBUUNUVDUUPUVFYFUUMUULEYEXMBUHUMUVFUVCXNBYEXMXKDUNUKULUOUPU
      VAUURUUSXIUUIUURUUJXGXHXJAUQURXIUUJUUSUUIXGXHXLAUQUSUTVAVBVCVCXNXPBUHVDVE
      UUGUUKYQUUNUAZYKXQUUDUUKUVGWRUUDUUKOYOUULYPUUMEUUDUUIYOUULUAZUUJBVFZAVFZX
      JAVGZKZUVHUUDUUIOXJBAVHYBUVIXIUUIXHYABVIZVJXIUVJYBUUIXGXHAVIZVKXIUUIUVLYB
      XIUVLUUIXIUVKXGXJXGXHAVLZVMVNVOVPURUUDUUJYPUUMUAZUUIUVIUVJXLUVKKZUVPUUDUU
      JOXLBAVHYBUVIXIUUJUVMVJXIUVJYBUUJUVNVKXIUUJUVQYBXIUVQUUJXIUVKXGXLUVOVMVNV
      OVPUSVQVOUFUUGUUKYRUUOUAZYKXQUUDWRUUKUVRUUDXOXGKZUVRWRUUKOUVIUVJXOUVKKZUV
      RUUDUVSOXOBAVHYBUVIXIUVSUVMVJXIUVJYBUVSUVNVKXIUVSUVTYBXIUVTUVSXIUVKXGXOUV
      OVMVNVOVPWRUUIUUJUVSXJXLCXGXGVRZVSVTWAWBUFWCWDWBWEWEVBWFWGVEUPVBWHWIWJXAX
      BXTXCYLWRWSXBXTWKWTFGACDXHXGUWAXHVRZWLWMWSWTXCYLWKWRHIBDEYAXHUWBYAVRZWLWN
      WOWRWTXFUUBWKWSFGXECEYAXGUWAUWCWLWPWQUP $.
      $( [25-Jun-2011] $) $( [1-Dec-2009] $)
  $}

  ${
    ghomdiv.1 $e |- X = ran G $.
    ghomdiv.2 $e |- D = ( /g ` G ) $.
    ghomdiv.3 $e |- C = ( /g ` H ) $.
    $( Group homomorphisms preserve division. $)
    ghomdiv $p |- ( ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) /\
                                ( A e. X /\ B e. X ) ) ->
                      ( F ` ( A D B ) ) = ( ( F ` A ) C ( F ` B ) ) ) $=
      ( cgr wcel cghom co w3a wa cfv wceq crn eqid grpnpcan 3simpl2 wf ffvelrn
      ghomf sylan adantrr adantrl syl3anc 3expb 3ad2antl1 fveq2d grpdivcl
      simprr jca ghomlin eqcomd syldan 3eqtr2rd wb grprcan syl3an2c mpbid ) FLM
      ZGLMZEFGNOMZPZAHMZBHMZQZQZABDOZERZBERZGOZAERZVOCOZVOGOZSZVNVRSZVLVSVQVMBF
      OZERZVPVFVQGTZMZVOWDMZVSVQSVLVQVOCGWDWDUAZKUBVEVFVGVKUCZVHVIWEVJHWDEUDZVI
      WEVHHWDAEUEEFGWDHIWGUFZUGUHZVHVJWFVIWIVJWFVHHWDBEUEWJUGUIZUJVLWBAEVEVFVKW
      BASZVGVEVIVJWMABDFHIJUBUKULUMVHVKVMHMZVJQZWCVPSVEVFVKWOVGVEVKQWNVJVEVIVJW
      NABDFHIJUNUKZVEVIVJUOUPULVHWOQVPWCVMBEFGHIUQURUSUTVFVNWDMZVRWDMZWFVTWAVAV
      LVNVRVOGWDWGVBWHVHVKWNWQVEVFVKWNVGWPULWIWNWQVHHWDVMEUEWJUGUSVFWEWFWRVLVQV
      OCGWDWGKUNWHWKWLUJWLVCVD $.
      $( [25-Jun-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d G x y $.  $d H x y $.  $d F x y $.  $d U x y $.  $d W x y $.
    $d X x y $.
    grpkerinj.1 $e |- X = ran G $.
    grpkerinj.2 $e |- W = ( Id ` G ) $.
    grpkerinj.3 $e |- Y = ran H $.
    grpkerinj.4 $e |- U = ( Id ` H ) $.
    $( A group homomorphism is injective if and only if its kernel is zero. $)
    grpkerinj $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ->
                      ( F : X -1-1-> Y <-> ( `' F " { U } ) = { W } ) ) $=
      ( vx vy cgr wcel cghom co w3a wf1 ccnv csn cima wceq wa cfv ghomid sneqd
      wfn fnsnfv wf ghomf ffn syl grpidcl 3ad2ant1 sylanc eqtr3d imaeq2d
      adantl wss f1imacnv snssi sylan2 eqtrd expcom cv wi wral dff13 adantr
      cgs wb eqid grpeqdivid 3simpl2 ffvelrn sylan adantrr adantrl syl3anc
      adantlr ghomdiv eqeq1d wfun cdm fvimacnv ffun grpdivcl 3expb 3ad2antl1
      fdm eleqtrrd eleq2 sylan9bb an1rs biimprd elsni syl5 sylbid cgi cvv fvex
      eqeltri snid eleq1 mpbiri sylbird r19.21aivva sylanbrc ex impbid ) CNOZDN
      OZBCDPQOZRZFGBSZBTZAUAZUBZEUAZUCZXPXOYAXPXOUDXSXQBXTUBZUBZXTXOXSYCUCXPXOX
      RYBXQXOEBUEZUAZXRYBXOYDAAEBCDIKUFUGBFUHZEFOZYEYBUCXOFEBUIXOFGBUJZYFBCDGFH
      JUKZFGBULUMXLXMYGXNECFHIUNZUOUPUQURUSXPXTFUTZYCXTUCXOFGXTBVAXLXMYKXNXLYGY
      KYJEFVBUMUOVCVDVEXOYAXPXPYHLVFZBUEZMVFZBUEZUCZYLYNUCZVGZMFVHLFVHXOYAUDZLM
      FGBVIXOYHYAYIVJYSYRLMFFYSYLFOZYNFOZUDZUDZYPYMYODVKUEZQZAUCZYQXOUUBYPUUFVL
      ZYAXMYMGOZYOGOZUUGXOUUBUDZYMYOUUDADGJKUUDVMZVNXLXMXNUUBVOXOYTUUHUUAYHYTUU
      HXOFGYLBVPYIVQVRXOUUAUUIYTYHUUAUUIXOFGYNBVPYIVQVSVTWAUUCUUFYLYNCVKUEZQZBU
      EZAUCZYQUUCUUNUUEAXOUUBUUNUUEUCYAYLYNUUDUULBCDFHUULVMZUUKWBWAWCUUCUUNXROZ
      YQUUOUUCUUQUUMXTOZYQXOUUBYAUUQUURVLUUJUUQUUMXSOZYAUURBWDZUUMBWEZOUUQUUSVL
      UUJUUMXRBWFXOUUTUUBXOYHUUTYIFGBWGUMVJUUJUUMFUVAXLXMUUBUUMFOZXNXLYTUUAUVBY
      LYNUULCFHUUPWHWIWJXOUVAFUCZUUBXOYHUVCYIFGBWKUMVJWLUPXSXTUUMWMWNWOXOUUBUUR
      YQVGYAUUJUUMEUCZYQUURXLXMUUBUVDYQVGZXNXLYTUUAUVEXLYTUUARYQUVDYLYNUULECFHI
      UUPVNWPWIWJUUMEWQWRWAWSUUOUUQAXROAADWTUEXAKDWTXBXCXDUUNAXRXEXFWRXGWSXHXIX
      JXK $.
      $( [25-Jun-2011] $) $( [16-Jun-2011] $)
  $}

