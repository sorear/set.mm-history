$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Groups and related structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    exidcl.1 $e |- X = ran G $.
    $( Closure of the binary operation of a magma with identity.  (Contributed
       by Jeff Madsen, 16-Jun-2011.) $)
    exidcl $p |- ( ( G e. ( Magma i^i ExId ) /\ A e. X /\ B e. X )
                                            -> ( A G B ) e. X ) $=
      ( cmagm cexid cin w3a co cdm wa crn rngopid syl5eq eleq2d anbi12d pm5.32i
      wcel inss1 sseli eqid clmgm syl3an1 3expb sylbi 3impb 3ad2ant1 eleqtrrd
      wceq ) CFGHZSZADSZBDSZIABCJZCKKZDULUMUNUOUPSZULUMUNLZLULAUPSZBUPSZLZLUQUL
      URVAULUMUSUNUTULDUPAULDCMUPECNOZPULDUPBVBPQRULUSUTUQULCFSUSUTUQUKFCFGTUAA
      BCUPUPUBUCUDUEUFUGULUMDUPUJUNVBUHUI $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d G x $.  $d Y x $.  $d X x $.  $d U u x y $.  $d H u x y $.
    exidres.1 $e |- X = ran G $.
    exidres.2 $e |- U = ( Id ` G ) $.
    exidres.3 $e |- H = ( G |` ( Y X. Y ) ) $.
    $( Lemma for ~ exidres and ~ exidresid . $)
    exidreslem $p |- ( ( G e. ( Magma i^i ExId ) /\ Y C_ X /\ U e. Y ) ->
                              ( U e. dom dom H /\ A. x e. dom dom H
                            ( ( U H x ) = x /\ ( x H U ) = x ) ) ) $=
      ( wcel wss cdm co wceq wa wral cxp sylib syl5eq dmeqd cmagm cexid cin w3a
      cv cres dmeqi xpss12 anidms wfo wf opidon2 fof fdm sseq2d syl5ibr ssdmres
      3syl imp dmxpid syl6eq eleq2d biimp3ar cmpidelt sylan2 anassrs adantrl wb
      ssel2 opreqi oprvres eqeq1d ancoms anbi12d adantl mpbird r19.21aiva 3impa
      3adant3 raleqdv jca ) CUAUBUCJZFEKZBFJZUDZBDLZLZJZBAUEZDMZWINZWIBDMZWINZO
      ZAWGPZWBWCWHWDWBWCOZWGFBWPWGFFQZLZFWPWFWQWPWFCWQUFZLZWQDWSIUGZWPWQCLZKZWT
      WQNZWBWCXCWCXCWBWQEEQZKZWCXFFEFEUHUIWBXBXEWQWBXEECUJXEECUKXBXENCEGULXEECU
      MXEECUNURUOUPUSZWQCUQZRSTFUTZVAVBVCWEWOWNAFPZWBWCWDXJWPWDOWNAFWPWDWIFJZWN
      WPWDXKOZOWNBWICMZWINZWIBCMZWINZOZWPXKXQWDWBWCXKXQWCXKOWBWIEJXQFEWIVIWIBCE
      GHVDVEVFVGXLWNXQVHWPXLWKXNWMXPXLWJXMWIXLWJBWIWSMXMDWSBWIIVJBWIFFCVKSVLXLW
      LXOWIXKWDWLXONXKWDOWLWIBWSMXODWSWIBIVJWIBFFCVKSVMVLVNVOVPVFVQVRWEWNAWGFWE
      WGWRFWEWFWQWEWFWTWQXAWEXCXDWBWCXCWDXGVSXHRSTXIVAVTVPWA $.
      $( [23-Dec-2013] $) $( [8-Jun-2010] $)

    $( The restriction of a binary operation with identity to a subset
       containing the identity has an identity element.  (Contributed by Jeff
       Madsen, 8-Jun-2010.) $)
    exidres $p |- ( ( G e. ( Magma i^i ExId ) /\ Y C_ X /\ U e. Y )
                                                      -> H e. ExId ) $=
      ( vu vx cexid wcel cv co wceq wa cdm wral eqeq1d cvv cmagm cin exidreslem
      wss w3a wrex opreq1 opreq2 anbi12d ralbidv rcla4ev syl wb cxp cres resexg
      syl5eqel eqid isexid 3ad2ant1 mpbird ) BUAKUBZLZEDUDZAELZUEZCKLZIMZJMZCNZ
      VIOZVIVHCNZVIOZPZJCQQZRZIVOUFZVFAVOLAVICNZVIOZVIACNZVIOZPZJVORZPVQJABCDEF
      GHUCVPWCIAVOVHAOZVNWBJVOWDVKVSVMWAWDVJVRVIVHAVICUGSWDVLVTVIVHAVICUHSUIUJU
      KULVCVDVGVQUMZVEVCCTLWEVCCBEEUNZUOTHBWFVBUPUQIJTCVOVOURUSULUTVA $.
      $( [23-Dec-2013] $) $( [8-Jun-2010] $)

    $( The restriction of a binary operation with identity to a subset
       containing the identity has the same identity element.  (Contributed by
       Jeff Madsen, 8-Jun-2010.) $)
    exidresid $p |- ( ( ( G e. ( Magma i^i ExId ) /\ Y C_ X /\ U e. Y ) /\
                                        H e. Magma ) -> ( Id ` H ) = U ) $=
      ( vu vx cmagm cexid wcel wa cv co wceq wral cvv adantr vy cin wss w3a cgi
      cfv crn crio cxp cres resexg syl5eqel eqid idrval syl 3ad2ant1 exidreslem
      cdm simprd exidres elin rngopid sylbir ancoms sylan raleqdv mpbird simpld
      wreu wb eleqtrrd exidu1 a17d opreq1 eqeq1d opreq2 anbi12d ralbidv riota2f
      ax-17 syl11anc mpbid eqtrd ) BKLUBZMZEDUCZAEMZUDZCKMZNZCUEUFZIOZJOZCPZWMQ
      ZWMWLCPZWMQZNZJCUGZRZIWSUHZAWHWKXAQZWIWEWFXBWGWECSMXBWECBEEUIZUJSHBXCWDUK
      ULJISWKCWSWSUMZWKUMUNUOUPTWJAWMCPZWMQZWMACPZWMQZNZJWSRZXAAQZWJXJXIJCURURZ
      RZWHXMWIWHAXLMZXMJABCDEFGHUQZUSTWJXIJWSXLWHCLMZWIWSXLQZABCDEFGHUTZWIXPXQW
      IXPNZCWDMZXQCKLVAZCVBVCVDVEZVFVGWJAWSMZWTIWSVIZXJXKVJWJAXLWSWHXNWIWHXNXMX
      OVHTYBVKWHXPWIYDXRWIXPYDXSXTYDYAJICWSXDVLVCVDVEWTXJIUAWSAUAOAMIVTYCXJIVMW
      LAQZWRXIJWSYEWOXFWQXHYEWNXEWMWLAWMCVNVOYEWPXGWMWLAWMCVPVOVQVRVSWAWBWC $.
      $( [23-Dec-2013] $) $( [8-Jun-2010] $)
  $}

  ${
    $d ph u x y z $.  $d G u n x y z $.  $d X u n x y z $.  $d U u n x y z $.
    $d N n $.
    isgrpda.1 $e |- ( ph -> X e. _V ) $.
    isgrpda.2 $e |- ( ph -> G : ( X X. X ) --> X ) $.
    isgrpda.3 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                            -> ( ( x G y ) G z ) = ( x G ( y G z ) ) ) $.
    isgrpda.4 $e |- ( ph -> U e. X ) $.
    isgrpda.5 $e |- ( ( ph /\ x e. X ) -> ( U G x ) = x ) $.

    ${
      isgrpda.6 $e |- ( ( ph /\ x e. X ) -> E. n e. X ( n G x ) = U ) $.
      $( Properties that determine a group operation.  (Contributed by Jeff
         Madsen, 1-Dec-2009.) $)
      isgrpda $p |- ( ph -> G e. GrpOp ) $=
        ( wcel co wceq wral wrex cvv vu cgr crn cxp wf cv wa w3a wi 3exp2 imp3a
        r19.21adv r19.21aivv opreq1 eqeq1d cbvrexv jca r19.21aiva eqeq2 rexbidv
        sylibr anbi12d ralbidv rcla4ev syl11anc 3jca wfo adantr simpr rcla4eopr
        eqcomd syl111anc fooprv sylanbrc forn xpeq1 3syl xpeq2 eqtrd feq2d feq3
        wb bitrd syl raleqdv raleqbidv rexeqdv anbi2d rexeqbidv 3anbi123d xpexg
        mpbird fex eqid isgrpo ) AGUBOZGUCZWQUDZWQGUEZBUFZCUFZGPDUFZGPWTXAXBGPZ
        GPQZDWQRZCWQRZBWQRZUAUFZWTGPZWTQZXAWTGPZXHQZCWQSZUGZBWQRZUAWQSZUHZAXQHH
        UDZHGUEZXDDHRZCHRZBHRZXJXLCHSZUGZBHRZUAHSZUHAXSYBYFJAXTBCHHAWTHOZXAHOZU
        GXDDHAYGYHXBHOZXDUIAYGYHYIXDKUJUKULUMAEHOZEWTGPZWTQZXKEQZCHSZUGZBHRZYFL
        AYOBHAYGUGZYLYNMYQFUFZWTGPZEQZFHSYNNYMYTCFHXAYRQXKYSEXAYRWTGUNUOUPVAUQU
        RYEYPUAEHXHEQZYDYOBHUUAXJYLYCYNUUAXIYKWTXHEWTGUNUOUUAXLYMCHXHEXKUSUTVBV
        CVDVEVFAWSXSXGYBXPYFAWSXRWQGUEZXSAWRXRWQGAWRHWQUDZXRAXRHGVGZWQHQZWRUUCQ
        AXSWTXCQDHSCHSZBHRUUDJAUUFBHYQYJYGWTYKQUUFAYJYGLVHAYGVIYQYKWTMVKCDHHEWT
        WTGVJVLURCDBHHHGVMVNZXRHGVOZWQHWQVPVQAUUDUUEUUCXRQUUGUUHWQHHVRVQVSVTAUU
        DUUEUUBXSWBUUGUUHWQHXRGWAVQWCAXFYABWQHAUUDUUEUUGUUHWDZAXEXTCWQHUUIAXDDW
        QHUUIWEWFWFAXOYEUAWQHUUIAXNYDBWQHUUIAXMYCXJAXLCWQHUUIWGWHWFWIWJWLAGTOZW
        PXQWBAXSXRTOZUUJJAHTOZUULUUKIIHHTTWKVEXRHTGWMVEBCDUATGWQWQWNWOWDWL $.
        $( [1-Dec-2009] $)
    $}

    ${
      isgrpod.6 $e |- ( ( ph /\ x e. X ) -> N e. X ) $.
      isgrpod.7 $e |- ( ( ph /\ x e. X ) -> ( N G x ) = U ) $.
      $( Properties that determine a group operation.  (Renamed from ~ isgrpd
         to ~ isgrpod to prevent naming conflict. -NM 5-Jun-2013) (Contributed
         by Jeff Madsen, 1-Dec-2009.) $)
      isgrpod $p |- ( ph -> G e. GrpOp ) $=
        ( vn cv wcel co wceq wa wrex opreq1 eqeq1d rcla4ev syl11anc isgrpda )
        ABCDEPFHIJKLMABQZHRUAGHRGUHFSZETZPQZUHFSZETZPHUBNOUMUJPGHUKGTULUIEUKGUH
        FUCUDUEUFUG $.
        $( [1-Dec-2009] $)
    $}

    ${
      isablda.6 $e |- ( ( ph /\ x e. X ) -> E. n e. X ( n G x ) = U ) $.
      isablda.7 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                              -> ( x G y ) = ( y G x ) ) $.
      $( Properties that determine an Abelian group operation.  (Contributed by
         Jeff Madsen, 11-Jun-2010.) $)
      isablda $p |- ( ph -> G e. AbelOp ) $=
        ( wcel cv co wceq cdm cgr crn wral cabl isgrpda wa grprndm syl wf dmeqd
        cxp dmxpid syl6eq eqtrd eleq2d anbi12d ex sylbid r19.21aivv eqid isablo
        fdm sylanbrc ) AGUAPZBQZCQZGRVFVEGRSZCGUBZUCBVHUCGUDPABCDEFGHIJKLMNUEZA
        VGBCVHVHAVEVHPZVFVHPZUFVEHPZVFHPZUFZVGAVJVLVKVMAVHHVEAVHGTZTZHAVDVHVPSV
        IGUGUHAVPHHUKZTHAVOVQAVQHGUIVOVQSJVQHGVBUHUJHULUMUNZUOAVHHVFVRUOUPAVNVG
        OUQURUSBCGVHVHUTVAVC $.
        $( [11-Jun-2010] $)
    $}

    ${
      isabld.6 $e |- ( ( ph /\ x e. X ) -> N e. X ) $.
      isabld.7 $e |- ( ( ph /\ x e. X ) -> ( N G x ) = U ) $.
      isabld.8 $e |- ( ( ph /\ ( x e. X /\ y e. X ) )
                                            -> ( x G y ) = ( y G x ) ) $.
      $( Properties that determine an Abelian group operation.  (Changed label
         from ~ isabld to ~ isablod -NM 6-Aug-2013.)  (Contributed by Jeff
         Madsen, 5-Dec-2009.) $)
      isablod $p |- ( ph -> G e. AbelOp ) $=
        ( vn cv wcel wceq wa co wrex opreq1 eqeq1d rcla4ev syl11anc isablda )
        ABCDEQFHIJKLMABRZHSUAGHSGUIFUBZETZQRZUIFUBZETZQHUCNOUNUKQGHULGTUMUJEULG
        UIFUDUEUFUGPUH $.
        $( [5-Dec-2009] $)
    $}
  $}

  ${
    abl4pnp.1 $e |- X = ran G $.
    abl4pnp.2 $e |- D = ( /g ` G ) $.
    $( A commutative/associative law for Abelian groups.  (Contributed by Jeff
       Madsen, 11-Jun-2010.) $)
    abl4pnp $p |- ( ( G e. AbelOp /\ ( ( A e. X /\ B e. X ) /\
                  ( C e. X /\ F e. X ) ) ) -> ( ( A G B ) D ( C G F ) ) =
                                            ( ( A D C ) G ( B D F ) ) ) $=
      ( cabl wcel wa co wceq w3a 3expib anim1d 3anass imp syldan df-3an opreq1d
      ablmuldiv sylan2br adantrrr cgr wi ablogrpo grpocl syl syl6ibr abldivdiv4
      grpdivcl an4 3imtr4g grpmuldivass sylan 3eqtr3d ) FJKZAGKZBGKZLZCGKZEGKZL
      ZLZLZABFMZCDMZEDMZACDMZBFMZEDMZVHCEFMDMZVKBEDMFMZVGVIVLEDUSVBVCVIVLNZVDVB
      VCLUSUTVAVCOVPUTVAVCUAABCDFGHIUCUDUEUBUSVFVHGKZVCVDOZVJVNNUSVFVRUSVFVQVEL
      VRUSVBVQVEUSFUFKZVBVQUGFUHZVSUTVAVQABFGHUIPUJQVQVCVDRUKSVHCEDFGHIULTUSVSV
      FVMVONZVTVSVFVKGKZVAVDOZWAVSVFWCVSUTVCLZVAVDLZLWBWELVFWCVSWDWBWEVSUTVCWBA
      CDFGHIUMPQUTVAVCVDUNWBVAVDRUOSVKBEDFGHIUPTUQUR $.
      $( [11-Jun-2010] $)
  $}

  ${
    grpeqdivid.1 $e |- X = ran G $.
    grpeqdivid.2 $e |- U = ( Id ` G ) $.
    grpeqdivid.3 $e |- D = ( /g ` G ) $.
    $( Two group elements are equal iff their quotient is the identity.
       (Contributed by Jeff Madsen, 6-Jan-2011.) $)
    grpeqdivid $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
                                              ( A = B <-> ( A D B ) = U ) ) $=
      ( cgr wcel w3a wceq co grpdivid 3adant2 opreq1 eqeq1d syl5ibrcom grpnpcan
      grpolid eqeq12d syl5ib impbid ) EJKZAFKZBFKZLZABMZABCNZDMZUHUKUIBBCNZDMZU
      EUGUMUFBCDEFGIHOPUIUJULDABBCQRSUKUJBENZDBENZMUHUIUJDBEQUHUNAUOBABCEFGITUE
      UGUOBMUFBDEFGHUAPUBUCUD $.
      $( [6-Jan-2011] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.  $d X x y $.
    ghomf.1 $e |- X = ran G $.
    ghomf.2 $e |- W = ran H $.
    $( Mapping property of a group homomorphism.  (Contributed by Jeff Madsen,
       1-Dec-2009.) $)
    ghomf $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpHom H ) )
                                                  -> F : X --> W ) $=
      ( vx vy cgr wcel cghom co wf wa cv cfv wceq wral elghom simprbda 3impa )
      BJKZCJKZABCLMKZEDANZUCUDOUEUFHPZAQIPZAQCMUGUHBMAQRIESHESHIABCDEFGTUAUB $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d S u v x y $.  $d T u v x y $.  $d G u v x y $.  $d H u v x y $.
    $d K u v x y $.
    $( The composition of two group homomorphisms is a group homomorphism.
       (Contributed by Jeff Madsen, 1-Dec-2009.) $)
    ghomco $p |- ( ( ( G e. GrpOp /\ H e. GrpOp /\ K e. GrpOp ) /\
                    ( S e. ( G GrpHom H ) /\ T e. ( H GrpHom K ) ) ) ->
                                      ( T o. S ) e. ( G GrpHom K ) ) $=
      ( vx vy vu vv cgr wcel cghom co wa cv cfv wceq wral wi adantlr w3a crn wf
      ccom fco ancoms ad2ant2r a1i ffvelrn adantrr adantrl fveq2 opreq1d opreq1
      jca fveq2d eqeq12d opreq2d opreq2 rcla42v imp sylan an32s adantllr anasss
      sylan9eq wfun cdm ffun ad2antlr ad2antrr fdm eleq2d biimpar fvco opreq12d
      syl111anc eqid grpocl 3expb sylan2 anassrs 3eqtr4d expr ralimdva ex com23
      com12 3ad2ant1 jcad wb elghom 3adant3 3adant1 anbi12d 3adant2 3imtr4d )
      CJKZDJKZEJKZUAZACDLMKZBDELMKZNZBAUDZCELMKZXACUBZDUBZAUCZFOZAPZGOZAPZDMZXJ
      XLCMZAPZQZGXGRZFXGRZNZXHEUBZBUCZHOZBPZIOZBPZEMZYCYEDMZBPZQZIXHRHXHRZNZNZX
      GYAXEUCZXJXEPZXLXEPZEMZXOXEPZQZGXGRZFXGRZNZXDXFXAYMYNUUAYMYNSXAXIYBYNXSYK
      YBXIYNXGXHYABAUEUFUGUHWRWSYMUUASWTYMWRUUAXIYLXSWRUUASZXIYLNXSUUCXIYBYKXSU
      UCSXIYBNZYKNZWRXSUUAUUEWRXSUUASZUUDWRYKUUFUUDWRNZYKNZXRYTFXGUUHXJXGKZNXQY
      SGXGUUHUUIXLXGKZXQYSSUUHUUIUUJNZXQYSUUHUUKXQNNXKBPZXMBPZEMZXPBPZYQYRUUHUU
      KXQUUNUUOQUUHUUKNXQUUNXNBPZUUOUUDYKUUKUUNUUPQZWRXIYKUUKUUQYBXIUUKYKUUQXIU
      UKNZXKXHKZXMXHKZNZYKUUQUURUUSUUTXIUUIUUSUUJXGXHXJAUIUJXIUUJUUTUUIXGXHXLAU
      IUKUOUVAYKUUQYJUUQUULYFEMZXKYEDMZBPZQHIXKXMXHXHYCXKQZYGUVBYIUVDUVEYDUULYF
      EYCXKBULUMUVEYHUVCBYCXKYEDUNUPUQYEXMQZUVBUUNUVDUUPUVFYFUUMUULEYEXMBULURUV
      FUVCXNBYEXMXKDUSUPUQUTVAVBVCVDVDXNXPBULVFVEUUGUUKYQUUNQZYKXQUUDUUKUVGWRUU
      DUUKNYOUULYPUUMEUUDUUIYOUULQZUUJUUDUUINBVGZAVGZXJAVHZKZUVHYBUVIXIUUIXHYAB
      VIZVJXIUVJYBUUIXGXHAVIZVKXIUUIUVLYBXIUVLUUIXIUVKXGXJXGXHAVLZVMVNTXJBAVOVQ
      UJUUDUUJYPUUMQZUUIUUDUUJNUVIUVJXLUVKKZUVPYBUVIXIUUJUVMVJXIUVJYBUUJUVNVKXI
      UUJUVQYBXIUVQUUJXIUVKXGXLUVOVMVNTXLBAVOVQUKVPTUGUUGUUKYRUUOQZYKXQUUDWRUUK
      UVRWRUUKNUUDXOXGKZUVRWRUUIUUJUVSXJXLCXGXGVRZVSVTUUDUVSNUVIUVJXOUVKKZUVRYB
      UVIXIUVSUVMVJXIUVJYBUVSUVNVKXIUVSUWAYBXIUWAUVSXIUVKXGXOUVOVMVNTXOBAVOVQWA
      WBUGWCWDWBWEWEVCWFWGVEVAVCWHWIWJXAXBXTXCYLWRWSXBXTWKWTFGACDXHXGUVTXHVRZWL
      WMWSWTXCYLWKWRHIBDEYAXHUWBYAVRZWLWNWOWRWTXFUUBWKWSFGXECEYAXGUVTUWCWLWPWQV
      A $.
      $( [1-Dec-2009] $)
  $}

  ${
    ghomdiv.1 $e |- X = ran G $.
    ghomdiv.2 $e |- D = ( /g ` G ) $.
    ghomdiv.3 $e |- C = ( /g ` H ) $.
    $( Group homomorphisms preserve division.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    ghomdiv $p |- ( ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpHom H ) ) /\
                                ( A e. X /\ B e. X ) ) ->
                      ( F ` ( A D B ) ) = ( ( F ` A ) C ( F ` B ) ) ) $=
      ( cgr wcel co wa cfv wceq ffvelrn sylan 3ad2antl1 cghom w3a crn simpl2 wf
      ghomf adantrr adantrl grpnpcan syl111anc 3expb fveq2d grpdivcl simprr jca
      eqid ghomlin eqcomd syldan 3eqtr2rd wb grporcan syl13anc mpbid ) FLMZGLMZ
      EFGUANMZUBZAHMZBHMZOZOZABDNZEPZBEPZGNZAEPZVOCNZVOGNZQZVNVRQZVLVSVQVMBFNZE
      PZVPVLVFVQGUCZMZVOWDMZVSVQQVEVFVGVKUDZVHVIWEVJVHHWDEUEZVIWEEFGWDHIWDUPZUF
      ZHWDAERSUGZVHVJWFVIVHWHVJWFWJHWDBERSUHZVQVOCGWDWIKUIUJVLWBAEVEVFVKWBAQZVG
      VEVIVJWMABDFHIJUIUKTULVHVKVMHMZVJOZWCVPQVEVFVKWOVGVEVKOWNVJVEVIVJWNABDFHI
      JUMUKZVEVIVJUNUOTVHWOOVPWCVMBEFGHIUQURUSUTVLVFVNWDMZVRWDMZWFVTWAVAWGVHVKW
      NWQVEVFVKWNVGWPTVHWHWNWQWJHWDVMERSUSVLVFWEWFWRWGWKWLVQVOCGWDWIKUMUJWLVNVR
      VOGWDWIVBVCVD $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d G x y $.  $d H x y $.  $d F x y $.  $d U x y $.  $d W x y $.
    $d X x y $.
    grpkerinj.1 $e |- X = ran G $.
    grpkerinj.2 $e |- W = ( Id ` G ) $.
    grpkerinj.3 $e |- Y = ran H $.
    grpkerinj.4 $e |- U = ( Id ` H ) $.
    $( A group homomorphism is injective if and only if its kernel is zero.
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    grpkerinj $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpHom H ) ) ->
                      ( F : X -1-1-> Y <-> ( `' F " { U } ) = { W } ) ) $=
      ( vx vy wcel co csn cima wceq wa cfv cgr cghom w3a wf1 ccnv ghomid wfn wf
      sneqd ffn syl grpoidcl 3ad2ant1 fnsnfv syl11anc eqtr3d imaeq2d adantl wss
      ghomf snssd f1imacnv sylan2 eqtrd expcom cv wi wral adantr cgs wb ffvelrn
      simpl2 sylan adantrr adantrl eqid grpeqdivid syl111anc adantlr eqeq1d cgi
      ghomdiv fvex eqeltri snid eleq1 mpbiri wfun ffun grpdivcl 3expb 3ad2antl1
      cvv cdm eleqtrrd fvimacnv eleq2 sylan9bb an32s elsni biimprd syl5 sylbird
      fdm sylbid r19.21aivva dff13 sylanbrc ex impbid ) CUANZDUANZBCDUBONZUCZFG
      BUDZBUEZAPZQZEPZRZXPXOYAXPXOSXSXQBXTQZQZXTXOXSYCRXPXOXRYBXQXOEBTZPZXRYBXO
      YDAAEBCDIKUFUIXOBFUGZEFNZYEYBRXOFGBUHZYFBCDGFHJUTZFGBUJUKXLXMYGXNECFHIULZ
      UMFEBUNUOUPUQURXOXPXTFUSZYCXTRXLXMYKXNXLEFYJVAUMFGXTBVBVCVDVEXOYAXPXOYASZ
      YHLVFZBTZMVFZBTZRZYMYORZVGZMFVHLFVHXPXOYHYAYIVIYLYSLMFFYLYMFNZYOFNZSZSZYQ
      YNYPDVJTZOZARZYRXOUUBYQUUFVKZYAXOUUBSZXMYNGNZYPGNZUUGXLXMXNUUBVMXOYTUUIUU
      AXOYHYTUUIYIFGYMBVLVNVOXOUUAUUJYTXOYHUUAUUJYIFGYOBVLVNVPYNYPUUDADGJKUUDVQ
      ZVRVSVTUUCUUFYMYOCVJTZOZBTZARZYRUUCUUNUUEAXOUUBUUNUUERYAYMYOUUDUULBCDFHUU
      LVQZUUKWCVTWAUUOUUNXRNZUUCYRUUOUUQAXRNAADWBTWNKDWBWDWEWFUUNAXRWGWHUUCUUQU
      UMXTNZYRXOUUBYAUUQUURVKUUHUUQUUMXSNZYAUURUUHBWIZUUMBWOZNUUQUUSVKXOUUTUUBX
      OYHUUTYIFGBWJUKVIUUHUUMFUVAXLXMUUBUUMFNZXNXLYTUUAUVBYMYOUULCFHUUPWKWLWMXO
      UVAFRZUUBXOYHUVCYIFGBXEUKVIWPUUMXRBWQUOXSXTUUMWRWSWTXOUUBUURYRVGYAUURUUME
      RZUUHYRUUMEXAXLXMUUBUVDYRVGZXNXLYTUUAUVEXLYTUUAUCYRUVDYMYOUULECFHIUUPVRXB
      WLWMXCVTXFXCXDXFXGLMFGBXHXIXJXK $.
      $( [16-Jun-2011] $)
  $}

