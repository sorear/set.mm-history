$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Euclidean space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The real numbers are a complete metric space. $)
    recms $p |- ( ( abs o. - ) |` ( RR X. RR ) ) e. CMet $=
      ( vy vx cabs cmin ccom cr cxp cres cms wcel copn cfv ccld cc cdif wss
      cc0 cv clt wbr cbl co wa wrex wral cme wb eqid cnmet cnmetba isopn4
      ax-mp difss cim eldifi imcl recn abscl syl 3syl cle wne ltlen 0re
      sylancr absge0 wn eldifn wceq reim0b necon3bbid mpbid abs00 necon3bid
      mpbird mpbir2and blssm a1i syl2anc sseld elbl c2 cexp cre caddc subcl
      syl2an recl sqge0 addge02 adantr resqcl sylanc absresq absvalsq2 imsub
      reim0 adantl opreq2d subid1 3eqtrd opreq1d eqtrd 3brtr4d le2sq cnmetdval
      breq2d lenlt metcl syl3anc bitr3d ex con2d adantld sylbid jcad eldif
      syl6ibr ssrdv breq2 opreq2 sseq1d anbi12d rcla4ev sylan32c rgen mpbir2an
      ctop opntop axresscn cuni uniopn2 eqcomd iscld2 mp2an mpbir cncms cmsss
      ) CDEZFFGHIJZFYSKLZMLJZUUBNFOZUUAJZUUDUUCNPZQARZSTZBRZUUFYSUALZUBZUUCPZUC
      ZAFUDZBUUCUEZYSUFJZUUDUUEUUNUCUGYSYSUHZUIZBAUUCYSUUANYSUUPUJZUUAUHZUKULNF
      UMUUMBUUCUUHUNLZCLZFJZQUVASTZUUHUVAUUIUBZUUCPZUUMUUHUUCJZUVFUUHNJZUUTFJZU
      VBUUHNFUOZUUHUPZUVHUUTNJZUVBUUTUQZUUTURUSUTZUVFUVCQUVAVATZUVAQVBZQFJUVBUV
      CUVNUVOUCUGUVFQUVAVCVDUVMVEUVFUVKUVNUVFUVGUVHUVKUVIUVJUVLUTZUUTVFUSZUVFUV
      OUUTQVBZUVFUUHFJZVGUVRUUHNFVHUVFUVSUUTQUVFUVGUVSUUTQVIZUGUVIUUHVJUSVKVLUV
      FUVAQUUTQUVFUVKUVAQVIUVTUGUVPUUTVMUSVNVOVPZUVFAUVDUUCUVFUUFUVDJZUUFNJZUUF
      FJZVGZUCUUFUUCJUVFUWBUWCUWEUVFUVDNUUFUUOUVGUVBUVCUVDNPUVFYSUUHUVANUURVQUU
      OUVFUUQVRZUVIUVMUWAVSVTUVFUWBUWCUUHUUFYSUBZUVASTZUCZUWEUUOUVGUVBUVCUWBUWI
      UGUVFUUFYSUUHUVANUURWAUWFUVIUVMUWAVSUVFUWHUWEUWCUVFUWDUWHUVFUWDUWHVGZUVFU
      WDUCZUVAUUHUUFDUBZCLZVATZUWJUWKUWNUVAWBWCUBZUWMWBWCUBZVATZUWKUUTWBWCUBZUW
      LWDLZWBWCUBZUWRWEUBZUWOUWPVAUWKQUWTVATZUWRUXAVATZUWKUWLNJZUWSFJZUXBUVGUWC
      UXDUVFUWDUUHUUFWFUVIUUFUQZWGZUWLWHZUWSWIUTUWRFJZUWTFJZUXBUXCUGUWKUWRUWTWJ
      UWKUVHUXIUVFUVHUWDUVFUVGUVHUVIUVJUSWKZUUTWLUSUWKUXDUXEUXJUXGUXHUWSWLUTWMV
      LUWKUVHUWOUWRVIUXKUUTWNUSUWKUWPUWTUWLUNLZWBWCUBZWEUBZUXAUWKUXDUWPUXNVIUXG
      UWLWOUSUWKUXMUWRUWTWEUWKUXLUUTWBWCUWKUXLUUTUUFUNLZDUBZUUTQDUBZUUTUVGUWCUX
      LUXPVIUVFUWDUUHUUFWPUVIUXFWGUWKUXOQUUTDUWDUXOQVIUVFUUFWQWRWSUWKUVKUXQUUTV
      IUVFUVKUWDUVPWKUUTWTUSXAXBWSXCXDUVBUVNUWMFJZQUWMVATZUWNUWQUGUWKUVAUWMXEUV
      FUVBUWDUVMWKZUVFUVNUWDUVQWKUWKUXDUXRUXGUWLURUSUWKUXDUXSUXGUWLVFUSVSVOUWKU
      VAUWGVATZUWNUWJUWKUWGUWMUVAVAUVGUWCUWGUWMVIUVFUWDUUHUUFYSUUPXFUVIUXFWGXGU
      VBUWGFJZUYAUWJUGUWKUVAUWGXHUXTUUOUVGUWCUYBUWKUUHUUFYSNUURXIUUOUWKUUQVRUVF
      UVGUWDUVIWKUWDUWCUVFUXFWRXJWMXKVLXLXMXNXOXPUUFNFXQXRXSUULUVCUVEUCAUVAFUUF
      UVAVIZUUGUVCUUKUVEUUFUVAQSXTUYCUUJUVDUUCUUFUVAUUHUUIYAYBYCYDYEYFYGUUAYHJZ
      FNPZUUBUUDUGUUOUYDUUQYSUUAUUSYIULYJFUUANUUONUUAYKZVIUUQUUOUYFNYSUUANUURUU
      SYLYMULYNYOYPYSIJUYEYTUUBUGYSUUPYQYJYSUUANFUURUUSYRYOYP $.
      $( [2-Sep-2009] $)
  $}

  $c Rn $.
  $( Extend class notation with the n-dimensional Euclidean space. $)
  crrn $a class Rn $.

  ${
    $d n r x y d k $.
    $( Define n-dimensional Euclidean space as a metric space with the standard
       Euclidean norm given by the quadratic mean. $)
    df-rrn $a |- Rn = { <. n , r >. | ( n e. NN /\ r = { <. <. x , y >. , d >.
          | ( ( x e. ( RR ^m ( 1 ... n ) ) /\ y e. ( RR ^m ( 1 ... n ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... n )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) } $.
  $}

  ${
    $d N n r x y d k $.
    $( The n-dimensional Euclidean space. $)
    rrnval $p |- ( N e. NN -> ( Rn ` N ) = { <. <. x , y >. , d >. |
            ( ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... N )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) $=
      ( vn vr cv cr c1 cfz co cmap wcel wa cfv cmin c2 cexp csu csqr wceq copab2
      cn crrn opreq2 opreq2d eleq2d anbi12d sumeq1d fveq2d eqeq2d oprabbidv
      df-rrn oprex eqid oprabex2 fvopab4 ) FGDAHZIJFHZKLZMLZNZBHZVBNZOZEHZVACHZ
      USPVHVDPQLRSLZCTZUAPZUBZOZABEUCUSIJDKLZMLZNZVDVONZOZVGVNVICTZUAPZUBZOZABE
      UCZUDUEUTDUBZVMWBABEWDVFVRVLWAWDVCVPVEVQWDVBVOUSWDVAVNIMUTDJKUFZUGZUHWDVB
      VOVDWFUHUIWDVKVTVGWDVJVSUAWDVAVNVICWEUJUKULUIUMABCFGEUNABEVOVOVTWCIVNMUOZ
      WGWCUPUQUR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.  $d X x y d k $.  $d Y x y d k $.
    $( The value of the Euclidean metric. $)
    rrnmval $p |- ( ( N e. NN /\ X e. ( RR ^m ( 1 ... N ) )
                  /\ Y e. ( RR ^m ( 1 ... N ) ) ) -> ( X ( Rn ` N ) Y ) =
        ( sqr ` sum_ k e. ( 1 ... N ) ( ( ( X ` k ) - ( Y ` k ) ) ^ 2 ) ) ) $=
      ( vx vy vd cn wcel cr c1 cfz co cmap crrn cfv cv cmin c2 cexp csu csqr
      wceq wa copab2 rrnval opreqd fvex fveq1 opreq1d sumeq2sdv fveq2d opreq2d
      eqid oprabval2 sylan9eq 3impb ) BHIZCJKBLMZNMZIZDUTIZCDBOPZMZUSAQZCPZVEDP
      ZRMZSTMZAUAZUBPZUCURVAVBUDVDCDEQZUTIFQZUTIUDGQUSVEVLPZVEVMPZRMZSTMZAUAZUB
      PZUCUDEFGUEZMVKURVCVTCDEFABGUFUGEFGCDUTUTVSVKVTUSVFVORMZSTMZAUAZUBPVJUBUH
      VLCUCZVRWCUBWDUSVQWBAWDVPWASTWDVNVFVORVEVLCUIUJUJUKULVMDUCZWCVJUBWEUSWBVI
      AWEWAVHSTWEVOVGVFRVEVMDUIUMUJUKULVTUNUOUPUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.
    $( The underlying set of Euclidean space. $)
    rrndm $p |- ( N e. NN -> dom dom ( Rn ` N ) = ( RR ^m ( 1 ... N ) ) ) $=
      ( vx vy vd vk cn wcel crrn cfv cdm cr c1 cfz co cmap cxp cv wa cmin c2
      cexp csu csqr wceq copab2 rrnval dmeqd fvex eqid dmoprab2 syl6eq c0 wne
      wn cc0 0re n0i ax-mp reex oprex map0 pm3.26bi mto df-ne mpbir a1i dmxp
      syl eqtrd ) AFGZAHIZJZJKLAMNZONZVNPZJZVNVJVLVOVJVLBQZVNGCQZVNGRDQVMEQZVQI
      VSVRISNTUANEUBZUCIZUDRBCDUEZJVOVJVKWBBCEADUFUGBCDVNVNWAWBVTUCUHWBUIUJUKUG
      VJVNULUMZVPVNUDWCVJWCVNULUDZUNWDKULUDZUOKGWEUNUPKUOUQURWDWEVMULUMKVMUSLAM
      UTVAVBVCVNULVDVEVFVNVNVGVHVI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k z m n $.
    $( Euclidean space is a metric space. $)
    rrnmet $p |- ( N e. NN -> ( Rn ` N ) e. Met ) $=
      ( vx vy vz vd vk vm vn crrn cfv cme wcel cdm cxp cr wf cv co cc0 wceq wb
      caddc cle wbr wral wa cn cvv fvex eqid ismet ax-mp c1 cfz cmap cmin c2
      cexp csu csqr copab2 w3a sqrcl cuz fsumrecl elnnuz biimpi 3ad2ant1
      resubcl ffvelrn reex oprex elmap sylanb 3ad2antl2 3ad2antl3 sylanc
      resqcl syl r19.21aiva fsumcmp0 sqge0 jca 3expib r19.21aivv foprab2 sylib
      rrnval feq1d mpbird rrndm xpeq1 xpeq2 eqtrd feq2d eleq2d anbi12d rrnmval
      eqeq1d sqr00 fsum00 cc recn sqeq0 3syl subeq0 recnd bitrd ralbidva wfn
      eqidd biantrurd eqfnfv bitr4d ffn sylbi syl2an 3adant1 3bitrd wi biimpd
      copab trirn pm3.26 3ad2antl1 fopab2 adantl syl3anc fveq2 opreq12d
      fvopab4 npncan adantll eqtr2d opreq1d sumeq2dv fveq2d cneg negsubdi2
      subcl sqneg eqcomd 3eqtr3d 3brtr4d 3anbi123i sylan2b 3adant3r3 3expb
      ancom2s 3adantr2 3adantr1 3exp2 3imp syld r19.21aiv sylbid sylanbrc ) AIJ
      ZKLZUURMMZUUTNZOUURPZBQZCQZUURRZSTZUVCUVDTZUAZUVEDQZUVCUURRZUVIUVDUURRZUB
      RZUCUDZDUUTUEZUFZCUUTUEBUUTUEZAUGLZUURUHLUUSUVBUVPUFUAAIUIBCDUHUURUUTUUTU
      JUKULUVQUVBOUMAUNRZUORZUVSNZOUURPZUVQUWAUVTOUVCUVSLZUVDUVSLZUFZEQUVRFQZUV
      CJZUWEUVDJZUPRZUQURRZFUSZUTJZTUFBCEVAZPZUVQUWKOLZCUVSUEBUVSUEUWMUVQUWNBCU
      VSUVSUVQUWBUWCUWNUWJOLZSUWJUCUDZUWNUVQUWBUWCVBZUWJVCAUMVDJLZUWIOLZFUVRUEU
      WOUWQUWIFUMAVEUVQUWBUWRUWCUVQUWRAVFVGVHZUWQUWSFUVRUWQUWEUVRLZUFZUWHOLZUWS
      UWFOLZUWGOLZUXCUXBUWFUWGVIUWBUVQUXAUXDUWCUVROUVCPZUXAUXDUWBUVROUWEUVCVJZO
      UVRUVCVKUMAUNVLZVMZVNVOZUWCUVQUXAUXEUWBUVROUVDPZUXAUXEUWCUVROUWEUVDVJZOUV
      RUVDVKUXHVMZVNVPZVQZUWHVRVSZVTVQZUWRUWSSUWIUCUDZUFZFUVRUEZUWPUWQUWIFUMAWA
      UWTUWQUXSFUVRUXBUWSUXRUXPUXBUXCUXRUXOUWHWBVSWCVTZVQZVQWDWEBCEUVSUVSUWKOUW
      LUWLUJWFWGUVQUVTOUURUWLBCFAEWHWIWJUVQUVAUVTOUURUVQUVAUVSUUTNZUVTUVQUUTUVS
      TZUVAUYCTAWKZUUTUVSUUTWLVSUVQUYDUYCUVTTUYEUUTUVSUVSWMVSWNWOWJUVQUVOBCUUTU
      UTUVQUVCUUTLZUVDUUTLZUFUWDUVOUVQUYFUWBUYGUWCUVQUUTUVSUVCUYEWPUVQUUTUVSUVD
      UYEWPWQUVQUWBUWCUVOUWQUVHUVNUWQUVFUWKSTZUWJSTZUVGUWQUVEUWKSFAUVCUVDWRZWSU
      WOUWPUYHUYIUAUWQUWJWTUXQUYBVQUWQUYIUWISTZFUVRUEZUWFUWGTZFUVRUEZUVGUWRUXTU
      YIUYLUAUWQUWIFUMAXAUWTUYAVQUWQUYKUYMFUVRUXBUYKUWHSTZUYMUXBUXCUWHXBLUYKUYO
      UAUXOUWHXCUWHXDXEUWFXBLZUWGXBLZUYOUYMUAUXBUWFUWGXFUXBUWFUXJXGUXBUWGUXNXGV
      QXHXIUWBUWCUYNUVGUAZUVQUVCUVRXJZUVDUVRXJZUYRUWBUWCUYSUYTUFZUYNUVRUVRTZUYN
      UFUVGVUAVUBUYNVUAUVRXKXLFUVRUVRUVCUVDXMXNUWBUXFUYSUXIUVROUVCXOXPUWCUXKUYT
      UXMUVROUVDXOXPXQXRXSXSUWQUVMDUUTUWQUVIUUTLZUVIUVSLZUVMUVQUWBVUCVUDXTUWCUV
      QVUCVUDUVQUUTUVSUVIUYEWPYAVHUVQUWBUWCVUDUVMXTUVQUWBUWCVUDUVMUVQUWBUWCVUDV
      BZUFZUWKUVRUWEUVIJZUWFUPRZUQURRZFUSZUTJZUVRVUGUWGUPRZUQURRZFUSZUTJZUBRZUV
      EUVLUCUVQUXFUXKUVROUVIPZVBZUWKVUPUCUDVUEUVQVURUFZUVRUWEGQZUVRLZHQZVUTUVCJ
      ZVUTUVIJZUPRZTUFGHYBZJZUWEVVAVVBVVDVUTUVDJZUPRZTUFGHYBZJZUBRZUQURRZFUSZUT
      JZUVRVVGUQURRZFUSZUTJZUVRVVKUQURRZFUSZUTJZUBRZUWKVUPUCUVQUVROVVFPZUVROVVJ
      PZVVOVWBUCUDVUSFAVVFVVJYCUVQVURYDVURVWCUVQVURVVEOLZGUVRUEVWCVURVWEGUVRVVC
      OLZVVDOLZVWEVURVVAUFZVVCVVDVIUXFUXKVVAVWFVUQUVROVUTUVCVJYEVUQUXFVVAVWGUXK
      UVROVUTUVIVJVPZVQVTGHUVROVVEVVFVVFUJZYFWGYGVURVWDUVQVURVVIOLZGUVRUEVWDVUR
      VWKGUVRVWGVVHOLZVWKVWHVVDVVHVIVWIUXKUXFVVAVWLVUQUVROVUTUVDVJVOVQVTGHUVROV
      VIVVJVVJUJZYFWGYGYHVUSUWJVVNUTVUSUVRUWIVVMFVUSUXAUFZUWHVVLUQURVWNVVLUWFVU
      GUPRZVULUBRZUWHUXAVVLVWPTVUSUXAVVGVWOVVKVULUBGHUWEVVEVWOUVRVVFVUTUWETZVVC
      UWFVVDVUGUPVUTUWEUVCYIVUTUWEUVIYIZYJVWJUWFVUGUPVLYKZGHUWEVVIVULUVRVVJVWQV
      VDVUGVVHUWGUPVWRVUTUWEUVDYIYJVWMVUGUWGUPVLYKZYJYGVURUXAVWPUWHTZUVQUYPVUGX
      BLZUYQVXAVURUXAUFZUWFVUGUWGYLVXCUWFUXFUXKUXAUXDVUQUXGYEXGZVXCVUGVUQUXFUXA
      VUGOLUXKUVROUWEUVIVJVPXGZVXCUWGUXKUXFUXAUXEVUQUXLVOXGYHYMYNYOYPYQVUSVUKVV
      RVUOVWAUBVUSVUJVVQUTVUSUVRVUIVVPFVURUXAVUIVVPTUVQVXCVUHYRZUQURRZVWOUQURRV
      UIVVPVXCVXFVWOUQURVXBUYPVXFVWOTVXCVUGUWFYSVXEVXDVQYOVXCVUHXBLZVXGVUITVXBU
      YPVXHVXCVUGUWFYTVXEVXDVQVUHUUAVSVXCVWOVVGUQURUXAVWOVVGTVURUXAVVGVWOVWSUUB
      YGYOUUCYMYPYQVUSVUNVVTUTVUSUVRVUMVVSFVWNVULVVKUQURUXAVULVVKTVUSUXAVVKVULV
      WTUUBYGYOYPYQYJUUDUWBUXFUWCUXKVUDVUQUXIUXMOUVRUVIVKUXHVMUUEUUFUVQUWBUWCUV
      EUWKTVUDUYJUUGVUFUVJVUKUVKVUOUBUVQUWBVUDUVJVUKTZUWCUVQVUDUWBVXIUVQVUDUWBV
      XIFAUVIUVCWRUUHUUIUUJUVQUWCVUDUVKVUOTZUWBUVQVUDUWCVXJUVQVUDUWCVXJFAUVIUVD
      WRUUHUUIUUKYJUUDUULUUMUUNUUOWCWDUUPWEUUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M k n $.  $d X k n $.  $d N k n $.  $d A k $.  $d F k n $.  $d G k n $.
    $d R k n $.
    rrndstprj1.1 $e |- M = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    rrndstprj1.2 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    $( The distance between two points in Euclidean space is greater than the
       distance between the projections onto one coordinate. $)
    rrndstprj1 $p |- ( ( ( N e. NN /\ A e. ( 1 ... N ) ) /\ ( F e. X /\ G e. X
                    ) ) -> ( ( F ` A ) M ( G ` A ) ) <_ ( F ( Rn ` N ) G ) ) $=
      ( vk cn wcel c1 cfz co wa cfv cmin cabs cv c2 cexp csu csqr crrn cle wbr
      cuz cr cc0 wral wceq fveq2 opreq12d opreq1d fsumlt1 elfzuz2 adantr
      resubcl wf ffvelrn cmap eleq2i reex oprex elmap bitri sylanb adantlr
      adantll sylanc resqcl syl sqge0 jca r19.21aiva simplr syl3anc ancoms
      sylan2b ad2ant2lr ad2ant2l absresq sqsqr fsumrecl fsumcmp0 3brtr4d wb
      le2sq cc recn abscl 3syl absge0 sqrcl sqrge0 syl2anc mpbird remetdval
      rrnmval simpll biimpi ad2antrl ad2antll ) EJKZALEMNZKZOZBFKZCFKZOZOZABPZA
      CPZQNZRPZXEISZBPZXPCPZQNZTUANZIUBZUCPZXLXMDNZBCEUDPNZUEXKXOYBUEUFZXOTUANZ
      YBTUANZUEUFZXKXNTUANZYAYFYGUEELUGPKZXTUHKZUIXTUEUFZOZIXEUJZXFYIYAUEUFXKXT
      YIIALEXPAUKZXSXNTUAYOXQXLXRXMQXPABULXPACULUMUNUOXGYJXJJALEUPUQZXKYMIXEXKX
      PXEKZOZYKYLYRXSUHKZYKXQUHKZXRUHKZYSYRXQXRURXJYQYTXGXHYQYTXIXEUHBUSZYQYTXH
      XEUHXPBUTXHBUHXEVANZKZUUBFUUCBHVBZUHXEBVCLEMVDZVEVFZVGVHVIXJYQUUAXGXIYQUU
      AXHXEUHCUSZYQUUAXIXEUHXPCUTXICUUCKZUUHFUUCCHVBZUHXECVCUUFVEVFZVGVIVIVJZXS
      VKVLZYRYSYLUULXSVMVLVNVOZXDXFXJVPVQXKXNUHKZYFYIUKXLUHKZXMUHKZUUOXKXLXMURX
      FXHUUPXDXIXFUUBUUPXHUUBXFUUPXEUHABUTVRUUGVSVTZXFXIUUQXDXHXFUUHUUQXIUUHXFU
      UQXEUHACUTVRUUKVSWAZVJZXNWBVLYAUHKZUIYAUEUFZYGYAUKXKYAWCYJYKIXEUJUVAXKXTI
      LEWDYPXKYKIXEUUMVOVJZYJYNUVBXKXTILEWEYPUUNVJZVJWFXOUHKZUIXOUEUFZYBUHKZUIY
      BUEUFZYEYHWGXKXOYBWHXKUUOXNWIKZUVEUUTXNWJZXNWKWLXKUUOUVIUVFUUTUVJXNWMWLUV
      AUVBUVGXKYAWNUVCUVDVJUVAUVBUVHXKYAWOUVCUVDVJWPWQUUPUUQYCXOUKXKXLXMDGWRUUR
      UUSVJXDUUDUUIYDYBUKXKIEBCWSXDXFXJWTXHUUDXGXIXHUUDUUEXAXBXIUUIXGXHXIUUIUUJ
      XAXCVQWF $.
      $( [2-Sep-2009] $)

    $( Bound on the distance between two points in Euclidean space given bounds
       on the distances in each coordinate.  This theorem and ~ rrndstprj1 can
       be used to show that the supremum norm and Euclidean norm are
       equivalent. $)
    rrndstprj2 $p |- ( ( ( N e. NN /\ F e. X /\ G e. X ) /\ ( R e. RR+ /\
                    A. n e. ( 1 ... N ) ( ( F ` n ) M ( G ` n ) ) < R ) ) ->
                                ( F ( Rn ` N ) G ) < ( R x. ( sqr ` N ) ) ) $=
      ( vk cn wcel cr c1 cfz co cmap w3a crp cv cfv clt wbr wral wa crrn csqr
      cmul cmin c2 cexp csu wceq rrnmval adantr cuz fsumlt elnnuz biimpi
      3ad2ant1 resubcl wf ffvelrn reex oprex elmap sylanb 3ad2antl2 3ad2antl3
      sylanc resqcl syl adantlr rpre ad2antlr wi cabs remetdval opreq1d
      absresq eqtrd ad2ant2r cc0 cle wb lt2sq cme remetba metcl remet a1i
      syl3anc metge0 rpge0 syl2anc biimpa anasss eqbrtrrd fveq2 opreq12d
      breq1d rcla4v imdistani ancoms sylan2 expr imp 3jca r19.21aiva caddc cc
      fsumconst rpcn sqcl syl2an npcan nncn ax1cn sylancl 3ad2antl1 adantrr
      breqtrd sqsqr fsumrecl fsumcmp0 sqge0 jca sqmul sqrcl nnre cn0 nnnn0
      nn0ge0 recnd mulcom 3eqtr3d 3brtr4d sqrge0 remulcl mulge0 mpbird eqbrtrd
      biid eleq2i 3anbi123i ) FKLZCMNFOPZQPZLZDUUHLZRZASLZBTZCUAZUUMDUAZEPZAUBU
      CZBUUGUDZUEZCDFUFUAPZAFUGUAZUHPZUBUCUUFCGLZDGLZRUUKUUSUEZUUTUUGJTZCUAZUVF
      DUAZUIPZUJUKPZJULZUGUAZUVBUBUUKUUTUVLUMUUSJFCDUNUOUVEUVLUVBUBUCZUVLUJUKPZ
      UVBUJUKPZUBUCZUVEUVKFAUJUKPZUHPZUVNUVOUBUVEUVKUUGUVQJULZUVRUBFNUPUALZUVJM
      LZUVQMLZUVJUVQUBUCZRZJUUGUDUVKUVSUBUCUVEUVJUVQJNFUQUUKUVTUUSUUFUUIUVTUUJU
      UFUVTFURUSZUTZUOUVEUWDJUUGUVEUVFUUGLZUEUWAUWBUWCUUKUWGUWAUUSUUKUWGUEZUVIM
      LZUWAUVGMLZUVHMLZUWIUWHUVGUVHVAUUIUUFUWGUWJUUJUUGMCVBUWGUWJUUIUUGMUVFCVCM
      UUGCVDNFOVEZVFVGVHZUUJUUFUWGUWKUUIUUGMDVBUWGUWKUUJUUGMUVFDVCMUUGDVDUWLVFV
      GVIZVJZUVIVKVLZVMUUSUWBUUKUWGUULUWBUURUULAMLZUWBAVNZAVKVLUOVOUVEUWGUWCUUK
      UULUURUWGUWCVPUUKUULUEZUURUWGUWCUWSUWGUVGUVHEPZAUBUCZUEZUWCUURUWGUEUWSUXB
      UEUWTUJUKPZUVJUVQUBUUKUWGUXCUVJUMUULUXAUWHUXCUVIVQUAZUJUKPZUVJUWHUWTUXDUJ
      UKUWJUWKUWTUXDUMUWHUVGUVHEHVRUWMUWNVJVSUWHUWIUXEUVJUMUWOUVIVTVLWAWBUWSUWG
      UXAUXCUVQUBUCZUWSUWGUEZUXAUXFUWTMLZWCUWTWDUCZUWQWCAWDUCZUXAUXFWEUXGUWTAWF
      EWGLZUWJUWKUXHUXGUVGUVHEMEHWHZWIUXKUXGEHWJWKZUUKUWGUWJUULUWMVMZUUKUWGUWKU
      ULUWNVMZWLUXKUWJUWKUXIUXGUVGUVHEMUXLWMUXMUXNUXOWLUULUWQUUKUWGUWRVOUULUXJU
      UKUWGAWNZVOWOWPWQWRUWGUURUXBUWGUURUXAUUQUXABUVFUUGUUMUVFUMZUUPUWTAUBUXQUU
      NUVGUUOUVHEUUMUVFCWSUUMUVFDWSWTXAXBXCXDXEXFWQXGXHXIVJUUKUULUVSUVRUMZUURUU
      FUUIUULUXRUUJUUFUULUEZUVSFNUIPNXJPZUVQUHPZUVRUVTUVQXKLZUVSUYAUMUUFUULUVQJ
      NFXLUWEUULAXKLZUYBAXMZAXNVLXOUXSUXTFUVQUHUUFUXTFUMZUULFXKLNXKLUYEUUFFNXPF
      XQXRXSUOVSWAXTYAYBUUKUVNUVKUMZUUSUVKMLZWCUVKWDUCZUYFUUKUVKYCUVTUWAJUUGUDU
      YGUUKUVJJNFYDUWFUUKUWAJUUGUWPXIVJZUVTUWAWCUVJWDUCZUEZJUUGUDUYHUUKUVJJNFYE
      UWFUUKUYKJUUGUWHUWAUYJUWPUWHUWIUYJUWOUVIYFVLYGXIVJZVJUOUUKUULUVOUVRUMZUUR
      UUFUUIUULUYMUUJUXSUVAAUHPZUJUKPZUVAUJUKPZUVQUHPZUVOUVRUVAXKLZUYCUYOUYQUMU
      UFUULUVAAYHUUFUVAFMLZWCFWDUCZUVAMLZUUFFYIFYJZUUFFYKLUYTFYLFYMVLZVJZYNZUYD
      XOUXSUYNUVBUJUKUYRUYCUYNUVBUMUUFUULUVAAYOVUEUYDXOVSUXSUYPFUVQUHUUFUYPFUMZ
      UULUYSUYTVUFUUFFYCVUBVUCVJUOVSYPXTYAYQUUKUULUVMUVPWEZUURUVLMLZWCUVLWDUCZU
      VBMLZWCUVBWDUCZVUGUWSUVLUVBWFUUKVUHUULUYGUYHVUHUUKUVKYIUYIUYLVJUOUUKVUIUU
      LUYGUYHVUIUUKUVKYRUYIUYLVJUOUUFUUIUULVUJUUJUULUUFVUJUWQVUAVUJUULUUFAUVAYS
      UWRVUDXOXDXTUUFUUIUULVUKUUJUULUUFVUKUWQUXJUEVUAWCUVAWDUCZUEVUKUULUUFAUVAY
      TUULUWQUXJUWRUXPYGUUFVUAVULVUDUYSUYTVULUUFFYRVUBVUCVJYGXOXDXTWOYAUUAUUBUU
      FUUFUVCUUIUVDUUJUUFUUCGUUHCIUUDGUUHDIUUDUUEVG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N f g j k m n r s x y z $.
    $( Euclidean space is complete. $)
    rrncms $p |- ( N e. NN -> ( Rn ` N ) e. CMet ) $=
      ( vf vx vm vz vn vy vj vk vr vg vs crrn cfv cms wcel cme cn cdm cv wf
      clm wbr wrex wi cca wral eqid iscms2 rrnmet wa wex c1 cfz co cr wceq
      copab cabs cmin ccom cxp cres cle clt crp ffvelrn adantrr adantrl jca
      adantlr adantll cmap rrndm adantr eleq2d anbi12d biimpd imdistani
      rrndstprj1 syl adantlrl lelttr remet remetba metcl mp3an1 reex oprex
      elmap syl6bb expcom sylan9 imp sylanc 3expb sylan rpre ad2antlr syl3anc
      mpand syldan anassrs fveq2 fveq1d fvex fvopab4 adantl opreq12d breq1d
      sylibrd imim2d ralimdvaa reximdva wb iscau5 simprl syl6rbb biimpar an1rs
      sylan2 r19.21aiva anasss ancom2s fopab2 sylib sylancr 3imtr4d eqcomi a1i
      breq1 rexeqbidv recms iscms mpbi pm3.27i vtoclri breq2 ac6s csqr cdiv
      eqcomd lmbrnns biimpa adantllr rpdivcl nnrp rpsqrcl ancoms imbi2d
      rexralbidv rcla4v sylbid cfn c0 wne wss filbcmb cuz elnnuz fzfi sylbi
      ad2antrr biimpi eluzfz1 ne0i 3syl nnssre ex w3a cmul 3ad2ant1 rrndstprj2
      exp32 syld imp32 cc cc0 divcan1 rpcn nnre cn0 nnnn0 nn0ge0 sqrcl recn
      rpne0 3ad2antl1 breqtrd expdimp an4s reximdv 3syld r19.21adva eqidd
      syl3an1 3expa imdistand biimprd syland eximdv mpd df-rex sylibr sylanbrc
      ) AMNZOPUXFQPZRUXFSSZBTZUAZUXICTZUXFUBNUCZCUXHUDZUEZBUXFUFNZUGARPZCUXFBUX
      HUXHUHZUIAUJZUXPUXNBUXOUXPUXIUXOPZUKZUXJUXMUXTUXJUKZUXKUXHPZUXLUKZCULZUXM
      UYAUMAUNUOZUPUXKUAZDTZRPZETFTZUYGUXINZNZUQUKDEURZUYIUXKNZUSUTVAUPUPVBVCZU
      BNZUCZFUYEUGZUKZCULZUYDUYAUYLGTZUYOUCZGUPUDZFUYEUGUYSUYAVUBFUYEUYAUYIUYEP
      ZUKUYLUYNUFNZPZVUBUXTUXJVUCVUEUXPUXJVUCUKZUXSVUEUXPVUFUKZUXSVUEVUGHTZITZV
      DUCZVUHUXINZVUIUXINZUXFUOZJTZVEUCZUEZIRUGZHRUDZJVFUGZVUJVUHUYLNZVUIUYLNZU
      YNUOZVUNVEUCZUEZIRUGZHRUDZJVFUGZUXSVUEVUGVURVVFJVFVUGVUNVFPZUKZVUQVVEHRVV
      IVUHRPZUKZVUPVVDIRVVKVUIRPZUKZVUOVVCVUJVVMVUOUYIVUKNZUYIVULNZUYNUOZVUNVEU
      CZVVCVVIVVJVVLVUOVVQUEZVVIVVJVVLUKZVUKUXHPZVULUXHPZUKZVVRVUGVVSVWBVVHVUFV
      VSVWBUXPUXJVVSVWBVUCUXJVVSUKVVTVWAUXJVVJVVTVVLRUXHVUHUXIVGVHUXJVVLVWAVVJR
      UXHVUIUXIVGZVIVJVKVLVKVVIVWBUKZVVPVUMVDUCZVUOVVQVUGVWBVWEVVHUXPVUCVWBVWEU
      XJUXPVUCUKZVWBUKZVWFVUKUPUYEVMUOZPZVULVWHPZUKZUKVWEVWFVWBVWKVWFVWBVWKVWFV
      VTVWIVWAVWJVWFUXHVWHVUKUXPUXHVWHUQZVUCAVNZVOZVPVWFUXHVWHVULVWNVPVQVRVSUYI
      VUKVULUYNAVWHUYNUHZVWHUHZVTWAWBVKVVPUPPZVUMUPPZVUNUPPZVWEVUOUKVVQUEVWDVVP
      VUMVUNWCVUGVWBVWQVVHUXPVUCVWBVWQUXJVVNUPPZVVOUPPZVWQVWGUYNQPZVWTVXAVWQUYN
      VWOWDZVVNVVOUYNUPUYNVWOWEZWFWGVWFVVTVWTVWAVWFVVTVWTUXPVVTUYEUPVUKUAZVUCVW
      TUXPVVTVXEUXPVVTVWIVXEUXPUXHVWHVUKVWMVPUPUYEVUKWHUMAUNWIZWJWKVRVXEVUCVWTU
      YEUPUYIVUKVGWLWMWNVHVWFVWAVXAVVTVWFVWAVXAUXPVWAUYEUPVULUAZVUCVXAUXPVWAVXG
      UXPVWAVWJVXGUXPUXHVWHVULVWMVPZUPUYEVULWHVXFWJWKVRVXGVUCVXAUYEUPUYIVULVGWL
      WMWNVIWOWBVKVUGVWBVWRVVHUXPVWBVWRVUFUXGVWBVWRUXPUXGVVTVWAVWRVUKVULUXFUXHU
      XQWFWPUXRWQVKVKVVHVWSVUGVWBVUNWRWSWTXAXBXCVVMVVBVVPVUNVEVVMVUTVVNVVAVVOUY
      NVVJVUTVVNUQVVIVVLDEVUHUYKVVNRUYLUYGVUHUQUYIUYJVUKUYGVUHUXIXDXEUYLUHZUYIV
      UKXFXGWSVVLVVAVVOUQVVKDEVUIUYKVVORUYLUYGVUIUQUYIUYJVULUYGVUIUXIXDXEVXIUYI
      VULXFXGZXHXIXJXKXLXMXNXMUXGUXJUXSVUSXOVUGJUXFHIUXIUXHUXQXPUXPUXGVUFUXRVOU
      XPUXJVUCXQWOVXBRUPUYLUAZVUEVVGXOVUGJUYNHIUYLUPVXDXPVXCVUGUYKUPPZDRUGZVXKU
      XPVUCUXJVXMUXPVUCUXJVXMVWFUXJUKVXLDRVWFUXJUYHVXLVWFUYJUXHPZVXLUXJUYHUKZUX
      PVXNVUCVXLUYEUPUYJUAZVUCVXLUXPVXNUKUYEUPUYIUYJVGZUXPVXPVXNUXPVXNUYJVWHPVX
      PUXPUXHVWHUYJVWMVPUPUYEUYJWHVXFWJXRXSZWQXTRUXHUYGUXIVGZYAXCYBYCYDDERUPUYK
      UYLVXIYEZYFYGYHWNXTXCKTZUYTUYOUCZGUYNSSZUDZVUBKUYLVUDVYAUYLUQZVYBVUAGVYCU
      PVYCUPUQVYEUPVYCVXDYIYJVYAUYLUYTUYOYKYLVXBVYDKVUDUGZUYNOPVXBVYFUKYMGUYNKV
      YCVYCUHYNYOYPYQWAYBVUAUYPFGUYEUPCVXFUYTUYMUYLUYOYRYSWAUYAUYRUYCCUYAUYBUYQ
      UYCUYFUYAUYBUYQUXLUXPUXJUYBUYQUXLUEZUEUXSUXPUXJUKZUYBVYGVYHUYBUKZUYQVUJVU
      LUXKUXFUOZVUNVEUCZUEZIRUGZHRUDZJVFUGZUXLVYIUYQVYNJVFVYIVVHUKZUYQVUJVVOUYM
      UYNUOZVUNAYTNZUUAUOZVEUCZUEZIRUGHRUDZFUYEUGZVUJVYTFUYEUGZUEZIRUGZHRUDZVYN
      VYPUYPWUBFUYEVYPVUCUKZUYPVUJVYQLTZVEUCZUEZIRUGHRUDZLVFUGZWUBVXBUYMUPPZVXK
      UYPWUMXOWUHLVVOUYNUYMHIUYLUPVXDVVLVVAVVOVXJUUBUUCVXBWUHVXCYJVYIVUCWUNVVHU
      XPUYBVUCWUNUXJUYFVUCWUNUXPUYBUKZUYEUPUYIUXKVGWUOUXKVWHPZUYFUXPUYBWUPUXPUX
      HVWHUXKVWMVPZUUDUPUYEUXKWHVXFWJZYFWQUUEVKWUHVXMVXKVYIVUCVXMVVHVYHVUCVXMUY
      BVYHVUCUKVXLDRVYHUYHVUCVXLVXPVUCVXLVYHUYHUKVXQUXPUXJUYHVXPUXPVXNVXPVXOVXR
      VXSYAXCWQXTYBVKVKVXTYFWTVYPWUMWUBUEZVUCVYHVVHWUSUYBUXPVVHWUSUXJUXPVVHUKZV
      YSVFPZWUSVVHUXPWVAVVHVYRVFPZWVAUXPVUNVYRUUFUXPAVFPZWVBAUUGZAUUHZWAYAZUUIW
      ULWUBLVYSVFWUIVYSUQZWUKWUAHIRRWVGWUJVYTVUJWUIVYSVYQVEYRUUJUUKUULWAVKVKVOU
      UMXMUYEUUNPZUYEUUOUUPZRUPUUQZWUCWUGUEVYPVYTFHIUYERUURVYHWVHUYBVVHUXPWVHUX
      JUXPAUMUUSNPZWVHAUUTZUMAUVAUVBVOUVCVYHWVIUYBVVHUXPWVIUXJUXPWVKUMUYEPWVIUX
      PWVKWVLUVDUMAUVEUYEUMUVFUVGVOUVCWVJVYPUVHYJWTVYPWUFVYMHRVYPWUEVYLIRVYPVVL
      UKZWUDVYKVUJWVMVYPVWAUKWUDVYKUEZVYPVVLVWAVYHVVLVWAUEZUYBVVHUXJWVOUXPUXJVV
      LVWAVWCUVIXHUVCVSVYIVVHVWAWVNUXPUYBVVHVWAUKWVNUXJUXPVVHUYBVWAWVNUXPUYBVWA
      UKZVVHWVNUXPVWJWUPUKZUKZVVHWVNUXPWVPUKWVRVVHWUDVYKUXPVWJWUPVVHWUDUKZVYKUE
      UXPVWJWUPUVJZWVSVYKWVTWVSUKVYJVYSVYRUVKUOZVUNVEWVTVVHWUDVYJWWAVEUCZWVTVVH
      WVAWUDWWBUEUXPVWJVVHWVAUEWUPVVHUXPWVAWVFWLUVLWVTWVAWUDWWBVYSFVULUXKUYNAVW
      HVWOVWPUVMUVNUVOUVPWVTVVHWWAVUNUQZWUDUXPVWJVVHWWCWUPVUNUVQPZVYRUVQPZVYRUV
      RUUPZWWCWUTVUNVYRUVSVVHWWDUXPVUNUVTXHUXPWWEVVHUXPAUPPZUVRAVDUCZUKVYRUPPWW
      EUXPWWGWWHAUWAUXPAUWBPWWHAUWCAUWDWAVJAUWEVYRUWFUVGVOUXPWWFVVHUXPWVCWVBWWF
      WVDWVEVYRUWGUVGVOWTUWHVHUWIUVIWPUWJUXPVWAUYBWVRUXPVWAUYBUKZWVQUXPWWIWVQUX
      PVWAVWJUYBWUPVXHWUQVQVRVSYDWQXTUWKUUEXCWAXLXMUWLUWMUWNUXPUYBUXJUXLVYOXOZU
      XPUYBUXJWWJUXGUYBUXJWWJUXPJVULUXFUXKHIUXIUXHUXQVVLVULUWOUUCUXRUWPUWQXTXKU
      VIVKUWRUYAUYBUYFUYAUYBWUPUYFUYAUXHVWHUXKUXPVWLUXSUXJVWMUVCVPWURWKUWSUWTUX
      AUXBUXLCUXHUXCUXDUVIYBUXE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X a b d f m n r u v w x y z $.  $d M a b d f m n r u v w x y z $.
    $d Y a b d f m n r u v w x y z $.  $d N a b d f m n r u v w x y z $.
    rrntotbnd.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrntotbnd.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem1 $p |- ( ( ( ( N e. NN /\ x e. X ) /\ ( d e. RR+ /\ r e. RR+ )
          ) /\ ( y e. dom dom ( Rn ` N ) /\ ( x ( Rn ` N ) y ) < r ) ) ->
        ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
            ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\
            v = ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( ball ` ( Rn ` N ) ) d ) e.
        { a | E. f ( f : ( 1 ... N ) --> ( -u ( ( |_ ` ( r / ( d / ( sqr ` N )
            ) ) ) + 1 ) ... ( ( |_ ` ( r / ( d / ( sqr ` N ) ) ) ) + 1 ) ) /\
                a = ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
                                ( ( d / ( sqr ` N ) ) x. ( f ` m ) ) ) ) }
                                            ( ball ` ( Rn ` N ) ) d ) ) } ) $=
      ( cn wcel cv wa crp crrn cfv cdm co clt wbr c1 cfz csqr cdiv cfl caddc
      cneg wf cmin c2 wceq copab cmul cbl wex cab oprex opabex2 feq1 fveq1
      opreq2d eqeq2d anbi2d opabbidv opreq1d anbi12d cla4ev wral cz cabs cle
      w3a wb cr absle zre nnre syl2an ancoms biimp3a elfz 3simp2 nnnegz
      3ad2ant1 nnz syl3anc mpbird cn0 rpdivcl simplr nnrp rpsqrcl syl sylan2
      adantlr sylanc cc0 flge0nn0 rpre rpge0 nn0p1nn 3syl ancom31s ad2antrr
      readdcl wne redivcl resubcl ffvelrn cmap rrndm eleq2d reex elmap syl6bb
      biimpa sylan adantllr eleq2i bitri sylanb adantll ad2ant2r rpne0 2re
      2ne0 rereccli sylancl flcl adantlrr cc zcn abscl recnd peano2zdi anasss
      peano2re absrdbnd flwordi absdiv absid eqtrd recn cme eqid metcl rrnmet
      syl6reqr adantr pm3.27 ad2antll ccom cxp cres remetdval remetba metsym
      remet a1i wi rrndstprj1 an1rs ex anassrs imp biimprd imdistani eqbrtrd
      eqbrtrrd ltle adantl ad2antlr impr letrd lediv1 elrp sylib mpbid leadd1
      1re r19.21aiva fopab2 eqidd eqeq1 exbidv elab sylibr ) IQRZASZJRZTZNSZUAR
      ZLSZUARZTZTZBSZIUBUCZUDUDZRZUWPUXEUXFUEZUXAUFUGZTZTZUHIUIUEZUXAUWSIUJUCZU
      KUEZUKUEZULUCZUHUMUEZUNZUXRUIUEZESZUOZFSZUXMRZDSZUYCUWPUCZUXOUYCGSZUXMRZC
      SUYGUXEUCZUYGUWPUCZUPUEZUXOUKUEZUHUQUKUEZUMUEZULUCZURTGCUSZUCZUTUEZUMUEZU
      RZTZFDUSZUWSUXFVAUCZUEZUYDUYEUYFUXOUYCUYAUCZUTUEZUMUEZURZTZFDUSZUWSVUCUEZ
      URZTZEVBZVUDUYBMSZVUKURZTZEVBZMVCRUXMUXTUYPUOZVUDVUDURZVUNUXLVUMVUSVUTTEU
      YPGCUXMUYOUHIUIVDZVEUYAUYPURZUYBVUSVULVUTUXMUXTUYAUYPVFVVBVUKVUDVUDVVBVUJ
      VUBUWSVUCVVBVUIVUAFDVVBVUHUYTUYDVVBVUGUYSUYEVVBVUFUYRUYFUMVVBVUEUYQUXOUTU
      YCUYAUYPVGVHVHVIVJVKVLVIVMVNUXLUYOUXTRZGUXMVOVUSUXLVVCGUXMUXRQRZUYOVPRZUY
      OVQUCZUXRVRUGZVVCUXLUYHTZVVDVVEVVGVSZVVCUXSUYOVRUGUYOUXRVRUGTZVVDVVEVVGVV
      JVVEVVDVVGVVJVTZUYOWARUXRWARZVVKVVEVVDUYOUXRWBUYOWCUXRWDWEWFWGVVEUXSVPRZU
      XRVPRZVVCVVJVTVVIUYOUXSUXRWHVVDVVEVVGWIVVDVVEVVMVVGUXRWJWKVVDVVEVVNVVGUXR
      WLWKWMWNUXDVVDUXKUYHUWOUXCVVDUWQUXCUWOVVDUWOUXBUWTVVDUWOUXBTUWTTZUXPUARZU
      XQWORZVVDUXBUXOUARZVVPVVOUXAUXOWPZUWOUXBUWTWQUWOUWTVVRUXBUWTUWOVVRUWTUXNU
      ARZVVRUWOUWSUXNWPUWOIUARVVTIWRIWSWTXAZWFZXBXCUXPWARZXDUXPVRUGVVQVVPUXPXEU
      XPXFZUXPXGXCUXQXHXIXJWFXBXKUXDUXHUYHVVEUXJUXDUXHTZUYHTZUYNWARZVVEUYLWARZU
      YMWARVWGVWFUYLUYMXLUYKWARZUXOWARZUXOXDXMZVWHVWFUYKUXOXNUWRUXHUYHVWIUXCUYI
      WARZUYJWARZVWIUWRUXHTZUYHTZUYIUYJXOUWOUXHUYHVWLUWQUXMWAUXEUOZUYHVWLUWOUXH
      TUXMWAUYGUXEXPUWOUXHVWPUWOUXHUXEWAUXMXQUEZRVWPUWOUXGVWQUXEIXRZXSWAUXMUXEX
      TVVAYAYBYCYDYEZUWRUYHVWMUXHUWQUYHVWMUWOUXMWAUWPUOZUYHVWMUWQUXMWAUYGUWPXPU
      WQUWPVWQRVWTJVWQUWPOYFWAUXMUWPXTVVAYAYGYHYIXBZXCYEZUXDVWJUXHUYHUWOUWTVWJU
      WQUXBUWTUWOVWJUWTUWOTZVVRVWJVWAUXOXFWTWFZYJXKUXDVWKUXHUYHUWOUWTVWKUWQUXBU
      WTUWOVWKVXCVVRVWKVWAUXOYKWTWFYJZXKWMZUQYLYMYNYOZUYNYPZWTYQVVHVVFUYLVQUCZU
      LUCZUHUMUEZUXRVVHUYOYRRZVVFWARVVHVWGVVEVXLUXDUXHUYHVWGUXJVXGYQVXHUYOYSXIU
      YOYTWTVVHVXIWARZVXKVPRVXKWARUXDUXHUYHVXMUXJVWFUYLYRRVXMVWFUYLVXFUUAUYLYTW
      TYQZVXMVXJVXIYPZUUBVXKWCXIVVHUXQWARZVVLUXDVXPUXKUYHUWOUXCVXPUWQUWOUXCTVWC
      UXQVPRVXPUWOUWTUXBVWCUXBUWOUWTTZVWCUXBVXQTVVPVWCUXBVVRVVPVXQVVSVWBXAVWDWT
      WFUUCZUXPYPUXQWCXIXBXKZUXQUUDWTVVHVWHVVFVXKVRUGUXDUXHUYHVWHUXJVXFYQUYLUUE
      WTVVHVXJUXQVRUGZVXKUXRVRUGZVXMVWCVXIUXPVRUGVXTVVHVXIUXPUUFVXNUXDVWCUXKUYH
      UWOUXCVWCUWQVXRXBXKVVHVXIUYKVQUCZUXOUKUEZUXPVRVVHVXIVYBUXOVQUCZUKUEZVYCUY
      KYRRZUXOYRRZVWKVXIVYEURVVHUYKUXOUUGVVHUYKUXDUXHUYHVWIUXJVXBYQZUUAUXDVYGUX
      KUYHUWOUWTVYGUWQUXBVXQUXOVXDUUAYJXKUXDVWKUXKUYHVXEXKWMVVHVYDUXOVYBUKUXDVY
      DUXOURZUXKUYHUWOUWTVYIUWQUXBVWJXDUXOVRUGZVYIVXQUXOUUHVXDVXQVVRVYJVWBUXOXG
      WTXCYJXKVHUUIVVHVYBUXAVRUGZVYCUXPVRUGZVVHVYBUXIUXAVVHVWIVYFVYBWARZVYHUYKU
      UJUYKYTXIZUXLUXIWARZUYHUWRUXHVYOUXCUXJUXFUUKRZUWPUXGRZUXHVYOVWNUWPUXEUXFU
      XGUXGUULUUMUWOVYPUWQUXHIUUNXKUWRVYQUXHUWOUWQVYQUWOJUXGUWPUWOUXGVWQJVWROUU
      OZXSYCUUPUWRUXHUUQWMZYJUUPUXDUXAWARZUXKUYHUXBVYTUWRUWTUXAXFZUURXKZUWRUXKU
      YHVYBUXIVRUGZUXCUWRUXHUYHWUCUXJVWOUYIUYJVQUPUUSWAWAUUTUVAZUEZVYBUXIVRVWLV
      WMWUEVYBURVWOUYIUYJWUDWUDUULZUVBVWSVXAXCVWOWUEUYJUYIWUDUEZUXIVRWUDUUKRZVW
      LVWMWUEWUGURVWOUYIUYJWUDWAWUDWUFUVCUVDWUHVWOWUDWUFUVEUVFVWSVXAWMUWRUXEJRZ
      TZUYHWUGUXIVRUGZVWNWUJUYHWUKUWOUWQWUIUYHWUKUVGUWOUWQWUITZTUYHWUKUWOUYHWUL
      WUKUYGUWPUXEWUDIJWUFOUVHUVIUVJUVKUVLUWRUXHWUIUWOUXHWUIUVGUWQUWOWUIUXHUWOJ
      UXGUXEVYRXSUVMUUPUVNYDUVOUVPYQYEUXLUXIUXAVRUGZUYHUXDUXHUXJWUMVYOVYTUXJWUM
      UVGVWEUXIUXAUVQUWRUXHVYOUXCVYSXBUXCVYTUWRUXHUXBVYTUWTWUAUVRUVSXCUVTUUPUWA
      VYMVYTVWJXDUXOUFUGTZVYKVYLVTVVHVYBUXAUXOUWBVYNWUBVVHVVRWUNUXDVVRUXKUYHUWO
      UWTVVRUWQUXBVWBYJXKUXOUWCUWDWMUWEUVOWMVXJWARZVXPUHWARZVXTVYAVTVVHVXJUXQUH
      UWFVVHVXMVXJVPRWUOVXNVXOVXJWCXIVXSWUPVVHUWGUVFWMUWEUWAWMUWHGCUXMUXTUYOUYP
      UYPUULUWIUWDUXLVUDUWJXCVURVUNMVUDVUBUWSVUCVDVUOVUDURZVUQVUMEWUQVUPVULUYBV
      UOVUDVUKUWKVJUWLUWMUWN $.
      $( [2-Sep-2009] $)

    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem2 $p |- ( ( ( N e. NN /\ d e. RR+ ) /\
          ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) ) ->
                  ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
        ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\ v =
                ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                      ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( Rn ` N ) y ) < d ) $=
      ( va cn wcel cv crp wa cr c1 cfz co cmap cfv csqr cdiv cmin c2 caddc cfl
      wceq copab cmul crrn clt w3a cabs ccom cxp cres wbr wral simpll wf
      readdcl ffvelrn reex oprex elmap sylanb adantll adantlrr remulcl rpdivcl
      nnrp rpsqrcl syl sylan2 rpre ancoms ad2antrr cz cc0 wne redivcl resubcl
      adantlr sylanc rpne0 syl3anc 2re 2ne0 rereccli sylancl flcl zre 3syl
      r19.21aiva eqid fopab2 sylib sylan sylibr simprr 3jca adantr fveq2
      opreq2d opreq12d fvopab4 opreq1d adantl fveq2d fvex remetdval simprl cc
      subcl recnd abscl a1i 1re cle rddif halflt1 lelttrd wb ltmul2 3exp exp4a
      3imp1 rpgt0 mpbid addcom recn ad2antrl subsub3 ad2antll divcan2 rpcn
      eqcomd subdi eqtr4d 3eqtr2d absmul absid rpge0 3eqtrd mulid1 3brtr4d
      eqbrtrd jca syl2an anassrs rrndstprj2 sylan32c divcan1 breqtrd ) HOPZKQZR
      PZSZAQZTUAHUBUCZUDUCZPZBQZUVFPZSZSZEQZUVEPZDQUVLUVDUEZUVAHUFUEZUGUCZUVLFQ
      ZUVEPZCQUVQUVHUEZUVQUVDUEZUHUCZUVPUGUCZUAUIUGUCZUJUCZUKUEZULSFCUMZUEZUNUC
      ZUJUCZULSEDUMZUVHHUOUEUCZUVPUVOUNUCZUVAUPUUTUWJUVFPZUVIUQUVPRPZNQZUWJUEZU
      WOUVHUEZURUHUSTTUTVAZUCZUVPUPVBZNUVEVCUWKUWLUPVBUVKUVKUUTUWMUVIUUTUVBUVJV
      DUVKUVETUWJVEZUWMUVKUWITPZEUVEVCUXAUVKUXBEUVEUVNTPZUWHTPZUXBUVKUVMSZUVNUW
      HVFUVCUVGUVMUXCUVIUVGUVMUXCUVCUVETUVDVEZUVMUXCUVGUVETUVLUVDVGTUVEUVDVHUAH
      UBVIZVJZVKVLVMUVPTPZUWGTPZUXDUXEUVPUWGVNUVCUXIUVJUVMUVBUUTUXIUVBUUTSZUWNU
      XIUVBUVORPZUWNUUTUVAUVOVOUUTHRPZUXLHVPZHVQZVRVSZUVPVTZVRWAZWBUVETUWFVEZUV
      MUXJUVKUVETUVLUWFVGUVKUWETPZFUVEVCUXSUVKUXTFUVEUVKUVRSZUWDTPZUWEWCPUXTUWB
      TPZUWCTPZUYBUYAUWBUWCVFUWATPZUXIUVPWDWEZUYCUYAUWAUVPWFUVJUVRUYEUVCUVSTPZU
      VTTPZUYEUVJUVRSUVSUVTWGUVIUVRUYGUVGUVETUVHVEZUVRUYGUVIUVETUVQUVHVGTUVEUVH
      VHUXGVJZVKVLUVGUVRUYHUVIUXFUVRUYHUVGUVETUVQUVDVGUXHVKWHWIVLUVCUXIUVJUVRUX
      RWBUVCUYFUVJUVRUVBUUTUYFUXKUWNUYFUXPUVPWJZVRWAWBWKUIWLWMWNZWOUWDWPUWEWQWR
      WSFCUVETUWEUWFUWFWTZXAXBXCWIWIWSEDUVETUWIUWJUWJWTZXAXBTUVEUWJVHUXGVJXDUVC
      UVGUVIXEXFUVCUWNUVJUVBUUTUWNUXPWAZXGUVKUWTNUVEUVKUWOUVEPZSZUWSUWOUVDUEZUV
      PUWOUWFUEZUNUCZUJUCZUWQUWRUCZUVPUPUYPUWSVUBULUVKUYPUWPVUAUWQUWREDUWOUWIVU
      AUVEUWJUVLUWOULZUVNUYRUWHUYTUJUVLUWOUVDXHVUCUWGUYSUVPUNUVLUWOUWFXHXIXJUYN
      UYRUYTUJVIXKXLXMUYQVUBUYRUVPUWQUYRUHUCZUVPUGUCZUWCUJUCZUKUEZUNUCZUJUCZUWQ
      UWRUCZUVPUPUYPVUBVUJULUVKUYPVUAVUIUWQUWRUYPUYTVUHUYRUJUYPUYSVUGUVPUNFCUWO
      UWEVUGUVEUWFUVQUWOULZUWDVUFUKVUKUWBVUEUWCUJVUKUWAVUDUVPUGVUKUVSUWQUVTUYRU
      HUVQUWOUVHXHUVQUWOUVDXHXJXLXLXNUYMVUFUKXOXKXIXIXLXMUVCUVJUYPVUJUVPUPVBZUW
      NUYRTPZUWQTPZSZVULUVCUVJUYPSZUWNVUOSZVUJVUIUWQUHUCZURUEZUVPUPVUITPZVUNVUJ
      VUSULVUQVUIUWQUWRUWRWTZXPVUMVUHTPZVUTVUQUYRVUHVFUWNVUMVUNXQUXIVUGTPZVVBVU
      QUVPVUGVNUWNUXIVUOUXQXGZVUQVUFTPZVUGWCPVVCVUETPZUYDVVEVUQVUEUWCVFVUDTPZUX
      IUYFVVFVUQVUDUVPWFVUOVVGUWNVUNVUMVVGUWQUYRWGWAXMZVVDUWNUYFVUOUYKXGZWKZUYL
      WOVUFWPVUGWQWRZWIZWIUWNVUMVUNXEWIVUQUVPVUGVUEUHUCZURUEZUNUCZUVPUAUNUCZVUS
      UVPUPVUQVVNUAUPVBZVVOVVPUPVBZVUQVVNUWCUAVUQVVMXRPZVVNTPZVUGXRPZVUEXRPZVVS
      VUQVUGVUEXSVUQVUGVVKXTZVUQVUEVVJXTZWIZVVMYAVRZUYDVUQUYLYBUATPZVUQYCYBZVUQ
      VVFVVNUWCYDVBVVJVUEYEVRUWCUAUPVBVUQYFYBYGVVTVWGUXIUQWDUVPUPVBZVVQVVRYHZVU
      QVVTVWGUXIVWIVWJVVTVWGUXIVWIVWJVVTVWGUXIVWISVWJVVNUAUVPYIYJYKYLVUQVVTVWGU
      XIVWFVWHVVDXFUWNVWIVUOUVPYMXGWIYNVUQVUSUVPVVMUNUCZURUEZUVPURUEZVVNUNUCZVV
      OVUQVURVWKURVUQVURVUHUYRUJUCZUWQUHUCZVUHVUDUHUCZVWKVUQVUIVWOUWQUHUYRXRPZV
      UHXRPZVUIVWOULVUQUYRVUHYOVUMVWRUWNVUNUYRYPYQZVUQVUHVVLXTZWIXLVWSUWQXRPZVW
      RVWQVWPULVUQVUHUWQUYRYRVXAVUNVXBUWNVUMUWQYPYSVWTWKVUQVWQVUHUVPVUEUNUCZUHU
      CZVWKVUQVUDVXCVUHUHVUQVXCVUDVUDXRPUVPXRPZUYFVXCVUDULVUQVUDUVPYTVUQVUDVVHX
      TUWNVXEVUOUVPUUAZXGZVVIWKUUBXIVXEVWAVWBVWKVXDULVUQUVPVUGVUEUUCVXGVWCVWDWK
      UUDUUEXNVXEVVSVWLVWNULVUQUVPVVMUUFVXGVWEWIVUQVWMUVPVVNUNUXIWDUVPYDVBZVWMU
      VPULVUQUVPUUGVVDUWNVXHVUOUVPUUHXGWIXLUUIVUQVVPUVPUWNVVPUVPULZVUOUWNVXEVXI
      VXFUVPUUJVRXGUUBUUKUULUYOVUPVUMVUNUVGUYPVUMUVIUXFUYPVUMUVGUVETUWOUVDVGUXH
      VKWHUVIUYPVUNUVGUYIUYPVUNUVIUVETUWOUVHVGUYJVKVLUUMUUNUUOUULUULWSUVPNUWJUV
      HUWRHUVFVVAUVFWTUUPUUQUVCUWLUVAULZUVJUVAXRPZUVOXRPZUVOWDWEZVXJUVCUVAUVOUU
      RUVBVXKUUTUVAUUAXMUUTVXLUVBUUTUXMUXLVXLUXNUXOUVOUUAWRXGUUTVXMUVBUUTUXMUXL
      VXMUXNUXOUVOWJWRXGWKXGUUS $.
      $( [2-Sep-2009] $)

    $( A set in Euclidean space is totally bounded iff its is bounded. $)
    rrntotbnd $p |- ( ( N e. NN /\ Y C_ X ) ->
                                      ( M e. TotBnd <-> M e. Bnd ) ) $=
      ( vx vr vv vb vy vd vf va vm vu vz vn cn wcel wss wa ctotbnd cbnd cdm
      eqid totbndbnd cv cbl cfv co wceq crp wrex wral cuni crrn cfn cxp cres
      a1i dmeqd cme metssba2 rrnmet adantr cr c1 cfz cmap rrndm syl6eqr sseq2d
      biimpar sylanc eqtr4d raleqdv c0 wi unieq raleq anbi12d rcla4ev emfin id
      0ss eqsstrd adantl ral0 jctir sylancr r19.21aiva a1d wne wn csqr cdiv
      cfl caddc cneg wf cmul copab wex cab eqeq1d sseq1 cin blssp inss1 jca
      sylan syl5cbir sylbird clt wbr wb cc0 elbl syl6reqr eleq2d biimpa
      rpregt0 syl2an adantrl cmin c2 eleq2 rrntotbndlem1 biimpd ad2antrr
      imdistani rrntotbndlem2 w3a elbl2 readdcl ffvelrn reex oprex elmap
      sylanb adantll adantlrr remulcl rpdivcl nnrp rpsqrcl syl sylan2 rpre
      ancoms cz redivcl resubcl adantlr rpne0 syl3anc 2re 2ne0 rereccli
      sylancl flcl zre 3syl fopab2 sylib syl6bb mpbird ad2ant2rl 3jca ad2antlr
      expr an1rs imp eleq2i biimpi anim2i sylanl1 adantrr eluni2 sylibr ex
      sylbid an4s ssel2 anim1i anasss anassrs ssrdv sstr expcom syld opreq1
      eqeq2d bitri cuz fzssuz uzssz sstri zssre fss mpan2 simprr 19.23adv
      visset eqeq1 anbi2d exbidv elab syl5ib r19.21aiv jctird cpw fixp elnnuz
      fzfi sylbi cn0 simplr cle flge0nn0 rpge0 nn0p1nn 1z znegcl ax-mp 0re 1re
      lt01 ltleii le0neg2 mpbi renegcli letri mp2an eluz mpbiri uzss sseli
      uzneg ax1cn negnegi syl5eqelr sseldd pwfi cvv mapsspw ssfi mapval
      firnfi4 adantllr adantlrl jctild syl6 r19.21adva r19.23advv r19.2z syl5
      expdimp df-ne sylan2br pm2.61dan metres syl5eqel isbnd2 sstotbnd 3imtr4d
      impbid2 ) BSTZDCUAZUBZAUCTZAUDTZAAUEZUEZVVOUFZUGVVKVVOGUHZHUHZAUIUJUKZULZ
      HUMUNZGVVOUOZDIUHZUPZUAZJUHZKUHZLUHZBUQUJZUIUJZUKZULZKVWIUEUEZUNZJVWCUOZU
      BZIURUNZLUMUOZVVMVVLVVKVWBVWAGDUOZVWRVVKVWAGVVODVVKVVOVWIDDUSUTZUEZUEZDVV
      KVVNVXAVVKAVWTAVWTULVVKFVAVBVBZVWIVCTZDVWMUAZDVXBULVVKVWIVWMDVWMUFZVDVVIV
      XDVVJBVEZVFZVVIVXEVVJVVIVWMCDVVIVWMVGVHBVIUKZVJUKZCBVKZEVLVMVNZVOZVPVQVVK
      DVRULZVWSVWRVSZVVKVXNUBZVWRVWSVXPVWQLUMVXPVWQVWHUMTZVRURTDVRUPZUAZVWNJVRU
      OZUBZVWQVXPVWPVYAIVRURVWCVRULZVWEVXSVWOVXTVYBVWDVXRDVWCVRVTVMVWNJVWCVRWAW
      BWCWDVXPVXSVXTVXNVXSVVKVXNDVRVXRVXNWEVRVXRUAVXNVXRWFVAWGWHVWNJWIWJWKVFWLW
      MVVKDVRWNZVXOVXNWOVVKVYCVWSVWRVVKVWAGDUNVWRVYCVWSUBVVKVVTVWRGHDUMVVKVVQDT
      ZVVRUMTZUBZVVTVWRVSVVKVYFUBZVVTVWQLUMVYGVXQUBZVVTVXIVVRVWHBWPUJZWQUKZWQUK
      ZWRUJZVHWSUKZWTZVYMVIUKZMUHZXAZNUHZOUHZVXITZPUHZVYSVVQUJZVYJVYSVYPUJZXBUK
      ZWSUKZULUBOPXCZVWHVWJUKZULZUBZMXDZNXEZURTZDWUKUPZUAZVWNJWUKUOZUBZUBVWQVYH
      VVTWUPWULVYHVVTWUNWUOVYHVVTDVVQVVRVWJUKZUAZWUNVYGVVTWURVSVXQVYGVVTDVVSULZ
      WURVYGDVVOVVSVVKDVVOULVYFVVKDVXBVVOVXMVXCVPVFXFWUSWURVVSWUQUAZVYGDVVSWUQX
      GVXDVXEUBZVYFWUTVVKWVAVYFUBZVVSWUQDXHZWUQVVRDVWIAVWMVVQVXFFXIWVCWUQUAWVBW
      UQDXJVAWGVVKVXDVXEVXHVXLXKXLXMXNVFVYHWUQWUMUAZWURWUNVSVYHKWUQWUMVVIVVQCTZ
      VYEUBZUBZVXQVWGWUQTZVWGWUMTZVSZVYGVVIVXQWVFWVJVVIWVEVXQVYEWVJVVIWVEUBZVXQ
      VYEUBUBZWVHVWGVWMTZVVQVWGVWIUKVVRXOXPZUBZWVIWVKVYEWVHWVOXQZVXQVXDVVQVWMTZ
      UBVVRVGTXRVVRXOXPUBWVPWVKVYEVWGVWIVVQVVRVWMVXFXSWVKVXDWVQVVIVXDWVEVXGVFVV
      IWVEWVQVVICVWMVVQVVIVWMVXJCVXKEXTYAYBXKVVRYCYDYEWVLWVOWVIWVLWVOUBZVWGQUHZ
      TZQWUKUNZWVIVYTWUAWUBVYJVYSRUHZVXITZVWCWWBVWGUJZWWBVVQUJZYFUKZVYJWQUKZVHY
      GWQUKZWSUKZWRUJZULUBRIXCZUJZXBUKZWSUKZULUBOPXCZVWHVWJUKZWUKTVWGWWPTZWWAWV
      RWVTWWQQWWPWUKWVSWWPVWGYHWCGKIPMORABCDHNLEFYIWVLWVMWWQWVNWVKVXQWVMWWQVYEW
      VKVXQUBZWVMUBWWRVWGVXJTZUBWWQWWRWVMWWSVVIWVMWWSVSWVEVXQVVIWVMWWSVVIVWMVXJ
      VWGVXKYAZYJYKYLVVIVVQVXJTZUBZVXQWWSWWQWVKWXBVXQUBWWSWWQVVIVXQWXAWWSWWQVSV
      VIVXQUBZWXAWWSWWQWXCWXAWWSUBZUBZWWQWWOVWGVWIUKVWHXOXPZGKIPORABCDLEFYMVXDW
      WOVWMTZWVMYNVWHVGTXRVWHXOXPUBZWWQWXFXQWXEVWGVWIWWOVWHVWMVXFYOWXEVXDWXGWVM
      VVIVXDVXQWXDVXGYKWXEWXGVXIVGWWOXAZWXEWWNVGTZOVXIUOWXIWXEWXJOVXIWUBVGTZWWM
      VGTZWXJWXEVYTUBZWUBWWMYPWXCWXAVYTWXKWWSWXAVYTWXKWXCVXIVGVVQXAZVYTWXKWXAVX
      IVGVYSVVQYQZVGVXIVVQYRVHBVIYSZYTZUUAUUBUUCVYJVGTZWWLVGTZWXLWXMVYJWWLUUDWX
      CWXRWXDVYTVXQVVIWXRVXQVVIUBZVYJUMTZWXRVXQVYIUMTZWYAVVIVWHVYIUUEVVIBUMTWYB
      BUUFBUUGUUHUUIZVYJUUJUUHUUKZYKVXIVGWWKXAZVYTWXSWXEVXIVGVYSWWKYQWXEWWJVGTZ
      RVXIUOWYEWXEWYFRVXIWXEWWCUBZWWIVGTZWWJUULTWYFWWGVGTZWWHVGTWYHWYGWWGWWHYPW
      WFVGTZWXRVYJXRWNZWYIWYGWWFVYJUUMWXDWWCWYJWXCWWDVGTZWWEVGTZWYJWXDWWCUBWWDW
      WEUUNWWSWWCWYLWXAVXIVGVWGXAWWCWYLWWSVXIVGWWBVWGYQVGVXIVWGYRWXPYTUUAUUBWXA
      WWCWYMWWSWXNWWCWYMWXAVXIVGWWBVVQYQWXQUUAUUOVOUUBWXCWXRWXDWWCWYDYKWXCWYKWX
      DWWCVXQVVIWYKWXTWYAWYKWYCVYJUUPUUHUUKYKUUQYGUURUUSUUTUVAWWIUVBWWJUVCUVDWL
      RIVXIVGWWJWWKWWKUFUVEUVFXLVOVOWLOPVXIVGWWNWWOWWOUFUVEUVFWXEWXGWWOVXJTWXIW
      XEVWMVXJWWOVVIVWMVXJULVXQWXDVXKYKYAVGVXIWWOYRWXPYTUVGUVHVVIWWSWVMVXQWXAVV
      IWVMWWSWWTVNUVIUVJVXQWXHVVIWXDVWHYCUVKVOUVHUVLUVMUVNWVEWXAVVIWVEWXACVXJVV
      QEUVOZUVPUVQUVRUUHUUCUVSVOQVWGWUKUVTUWAUWBUWCUWDUVMVVIVVJVYFWVGVVJVYFUBWV
      FVVIVVJVYDVYEWVFVVJVYDUBZWVEVYEDCVVQUWEZUWFUWGUVQUWHXLUWIWURWVDWUNDWUQWUM
      UWJUWKUUHUWLVYHVWNJWUKVYHVYQVWFWUGULZUBZMXDZVWNVWFWUKTVYHWYRVWNMVYHWYRVWN
      WUFVWMTZWYQVWNVYHWYRUBVWLWYQKWUFVWMVWGWUFULVWKWUGVWFVWGWUFVWHVWJUWMUWNWCV
      YHVYQWYTWYQVYHVYQUBZWYTVXIVGWUFXAZXUAWUEVGTZOVXIUOXUBXUAXUCOVXIVYHVYQVYTX
      UCWXKWUDVGTZXUCVYHVYQVYTUBZUBWUBWUDYPVYGVYTWXKVXQVYQVVKVYDVYTWXKVYEWXNVYT
      WXKVVKVYDUBWXOVVIVVJVYDWXNVVIWVEWXNWYOWVEWXNVVIWVEWXNWVEWXAWXNWYNWXQUWOUV
      PWHWYPUUIUWHXLUUCUVIWXRWUCVGTZXUDVYHXUEVYJWUCUUDVVKVXQWXRVYFVVIVXQWXRVVJW
      YDUUOUUOVXIVGVYPXAZVYTXUFVYQVXIVGVYSVYPYQVYQVYOVGUAXUGVYOUULVGVYOVYNUWPUJ
      ZUULVYNVYMUWQVYNUWRUWSUWTUWSVXIVYOVGVYPUXAUXBXLYDVOUWHWLOPVXIVGWUEWUFWUFU
      FUVEUVFVYGWYTXUBXQZVXQVYQVVIXUIVVJVYFVVIWYTWUFVXJTXUBVVIVWMVXJWUFVXKYAVGV
      XIWUFYRWXPYTUVGYKYKUVHUVSVYHVYQWYQUXCVOUWBUXDWUJWYSNVWFJUXEVYRVWFULZWUIWY
      RMXUJWUHWYQVYQVYRVWFWUGUXFUXGUXHUXIUXJUXKUXLVVKVYEVXQWULVYDVVIVYEVXQWULVV
      JVVIVYEUBVXQUBZVXIVYOUSZUXMZURTZVYQMXEZURTWULXUKXULURTZXUNVXIURTZVYOURTZX
      UPXUKVXIVYOUXNVVIXUQVYEVXQVVIBVHUWPUJZTXUQBUXOVHBUXPUXQYKXUKVYMSTZVYMXUHT
      XURXUKVYKUMTZVYLUXRTZXUTVYEWYAXVAXUKVVRVYJUUEVVIVYEVXQUXSVVIVXQWYAVYEVXQV
      VIWYAWYCUUKUUOVOVYKVGTXRVYKUXTXPXVBXVAVYKUYAVYKUUJVYKUYBVOVYLUYCUVDXUTXUS
      XUHVYMXUTVHXUHTXUSXUHUAXUTVHWTZWTZXUHVHXUTVYMXVCUWPUJZTZXVDXUHTXUTVYMXUST
      ZXVFVYMUXOZXUSXVEVYMVHXVETZXUSXVEUAXVCUULTZVHUULTZXVIXVKXVJUYDVHUYEUYFUYD
      XVJXVKUBXVIXVCVHUXTXPZXVCXRUXTXPZXRVHUXTXPZXVLXVNXVMXRVHUYGUYHUYIUYJZVHVG
      TXVNXVMXQUYHVHUYKUYFUYLXVOXVCXRVHVHUYHUYMUYGUYHUYNUYOXVCVHUYPUYQUYOXVCVHU
      YRUYFUYSUXQXVCVYMUYTUUHVHVUAVUBVUCVYNVHUYRUUHXUTXVGXVHUVPVUDVYNVYMUXPUVDV
      OXULVUEUVFXUNVYOVXIVJUKZURXUOXUNXVPXUMUAZXVPURTVXIVUFTXVQWXPVYOVXIVUFVUGU
      YFXUMXVPVUHUXBVYOVXIMVYNVYMVIYSWXPVUIVUCVYQNMWUGVUJUVDVUKVULVUMVWPWUPIWUK
      URVWCWUKULZVWEWUNVWOWUOXVRVWDWUMDVWCWUKVTVMVWNJVWCWUKWAWBWCVUNVUOUWBVUPVW
      AGDVUQVURVUSDVRVUTVVAVVBUWCVVKVXDAVCTVVMVWBXQVXHVXDVWTVCAVWIDVVCFVVDGAVVO
      HVVPVVEUVDVXDVXEVVLVWRXQVVKKIVWIAVWMDJLVXFFVVFVXHVXLVOVVGVVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    rrnheibor.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrnheibor.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    rrnheibor.3 $e |- T = ( Open ` M ) $.
    rrnheibor.4 $e |- U = ( Open ` ( Rn ` N ) ) $.
    $( Heine-Borel theorem for Euclidean space.  A subset of Euclidean space is
       compact iff it is closed and bounded. $)
    rrnheibor $p |- ( ( N e. NN /\ Y C_ X ) ->
                      ( T e. Comp <-> ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( cn wcel wss wa ccomp cms ctotbnd ccld cfv cbnd cme wb crrn rrnmet cxp
      cres metres syl5eqel syl adantr cdm eqid heibor cmsss rrncms cr c1 cfz
      co cmap rrndm syl6reqr sseq2d biimpa sylanc eleq1i syl5bb rrntotbnd
      anbi12d bitrd ) DKLZFEMZNZAOLZCPLZCQLZNZFBRSLZCTLZNVMCUALZVNVQUBVKVTVLVKD
      UCSZUALZVTDUDWBWAFFUEUFZUACWAFUGHUHUIUJACCUKUKZIWDULUMUIVMVOVRVPVSVMWCPLZ
      VRVOWAPLZFWAUKUKZMZWEVRUBVMWABWGFWGULJUNVKWFVLDUOUJVKVLWHVKEWGFVKWGUPUQDU
      RUSUTUSEDVAGVBVCVDVECWCPHVFVGCDEFGHVHVIVJ $.
      $( [2-Sep-2009] $)
  $}

