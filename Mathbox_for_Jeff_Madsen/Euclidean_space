$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Euclidean space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The real numbers are a complete metric space.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    recms $p |- ( ( abs o. - ) |` ( RR X. RR ) ) e. CMet $=
      ( vx vy cmin cr wcel cfv cc wss co wa wb cc0 3syl wceq syl wbr cle adantr
      c2 cexp cabs ccom cxp cres cms copn ccld cdif cbl crp wrex wral cme cnmet
      cv eqid cnmetba isopn4 ax-mp difss cim wne eldifi imcl recn eldifn reim0b
      wn necon3bbid mpbid absrpcl syl2anc a1i blssm syl3anc sseld clt cre caddc
      elbl subcl syl2an recl sqge0 resqcl addge02 absresq absvalsq2 imsub reim0
      adantl opreq2d subid1 3eqtrd opreq1d eqtrd 3brtr4d absge0 syl22anc mpbird
      abscl le2sq cnmetdval breq2d metcl lenlt bitr3d con2d adantld sylbid jcad
      ex eldif syl6ibr ssrdv opreq2 sseq1d rcla4ev rgen mpbir2an ctop ax-resscn
      opntop cuni uniopn2 iscld2 mp2an mpbir wtru cncms cmsss trud ) UACUBZDDUC
      UDUEEZDYMUFFZUGFEZYPGDUHZYOEZYRYQGHZAUOZBUOZYMUIFZIZYQHZBUJUKZAYQULZYMUME
      ZYRYSUUFJKYMYMUPZUNZABYQYMYOGYMUUHUQZYOUPZURUSGDUTUUEAYQYTYQEZYTVAFZUAFZU
      JEZYTUUNUUBIZYQHZUUEUULUUMGEZUUMLVBZUUOUULYTGEZUUMDEZUURYTGDVCZYTVDZUUMVE
      ZMZUULYTDEZVHUUSYTGDVFUULUVFUUMLUULUUTUVFUUMLNKUVBYTVGOVIVJUUMVKVLZUULBUU
      PYQUULUUAUUPEZUUAGEZUUADEZVHZJUUAYQEUULUVHUVIUVKUULUUPGUUAUULUUGUUTUUOUUP
      GHUUGUULUUIVMZUVBUVGYMYTUUNGUUJVNVOVPUULUVHUVIYTUUAYMIZUUNVQPZJZUVKUULUUG
      UUTUUOUVHUVOKUVLUVBUVGUUAYMYTUUNGUUJVTVOUULUVNUVKUVIUULUVJUVNUULUVJUVNVHZ
      UULUVJJZUUNYTUUACIZUAFZQPZUVPUVQUVTUUNSTIZUVSSTIZQPZUVQUUMSTIZUVRVRFZSTIZ
      UWDVSIZUWAUWBQUVQLUWFQPZUWDUWGQPZUVQUVRGEZUWEDEZUWHUULUUTUVIUWJUVJUVBUUAV
      EZYTUUAWAWBZUVRWCZUWEWDMUVQUWDDEZUWFDEZUWHUWIKUVQUVAUWOUULUVAUVJUULUUTUVA
      UVBUVCORZUUMWEOUVQUWJUWKUWPUWMUWNUWEWEMUWDUWFWFVLVJUVQUVAUWAUWDNUWQUUMWGO
      UVQUWBUWFUVRVAFZSTIZVSIZUWGUVQUWJUWBUWTNUWMUVRWHOUVQUWSUWDUWFVSUVQUWRUUMS
      TUVQUWRUUMUUAVAFZCIZUUMLCIZUUMUULUUTUVIUWRUXBNUVJUVBUWLYTUUAWIWBUVQUXALUU
      MCUVJUXALNUULUUAWJWKWLUVQUURUXCUUMNUULUURUVJUVERUUMWMOWNWOWLWPWQUVQUUNDEZ
      LUUNQPZUVSDEZLUVSQPZUVTUWCKUULUXDUVJUULUUTUVAUXDUVBUVCUVAUURUXDUVDUUMXAOM
      RZUULUXEUVJUULUURUXEUVEUUMWRORUVQUWJUXFUWMUVRXAOUVQUWJUXGUWMUVRWROUUNUVSX
      BWSWTUVQUUNUVMQPZUVTUVPUVQUVMUVSUUNQUULUUTUVIUVMUVSNUVJUVBUWLYTUUAYMUUHXC
      WBXDUVQUXDUVMDEZUXIUVPKUXHUVQUUGUUTUVIUXJUUGUVQUUIVMUULUUTUVJUVBRUVJUVIUU
      LUWLWKYTUUAYMGUUJXEVOUUNUVMXFVLXGVJXLXHXIXJXKUUAGDXMXNXOUUDUUQBUUNUJUUAUU
      NNUUCUUPYQUUAUUNYTUUBXPXQXRVLXSXTYOYAEZDGHZYPYRKUUGUXKUUIYMYOUUKYCUSYBDYO
      GUUGGYOYDNUUIYMYOGUUJUUKYEUSYFYGYHYNYPKYIYMYOGDUUJUUKYMUEEYIYMUUHYJVMUXLY
      IYBVMYKYLYH $.
      $( [2-Sep-2009] $)
  $}

  $c Rn $.
  $( Extend class notation with the n-dimensional Euclidean space. $)
  crrn $a class Rn $.

  ${
    $d n r x y d k $.
    $( Define n-dimensional Euclidean space as a metric space with the standard
       Euclidean norm given by the quadratic mean. $)
    df-rrn $a |- Rn = ( n e. NN |->
       ( x e. ( RR ^m ( 1 ... n ) ) , y e. ( RR ^m ( 1 ... n ) ) |-> ( sqr `
         sum_ k e. ( 1 ... n ) ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) ) $.
  $}

  ${
    $d N n r x y d k $.
    $( The n-dimensional Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnval $p |- ( N e. NN -> ( Rn ` N ) =
        ( x e. ( RR ^m ( 1 ... N ) ) , y e. ( RR ^m ( 1 ... N ) ) |-> ( sqr `
          sum_ k e. ( 1 ... N ) ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) ) $=
      ( vn cr c1 cv cfz co cmap cfv cmin c2 cexp csu csqr cmpt2 cn crrn opreq2d
      wceq opreq2 sumeq1d fveq2d mpt2eq123dv df-rrn oprex mpt2ex fvmpt ) EDABFG
      EHZIJZKJZUMULCHZAHLUNBHLMJNOJZCPZQLZRABFGDIJZKJZUSURUOCPZQLZRSTUKDUBZABUM
      UMUQUSUSVAVBULURFKUKDGIUCZUAZVDVBUPUTQVBULURUOCVCUDUEUFABCEUGABUSUSVAFURK
      UHZVEUIUJ $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.  $d X x y d k $.  $d Y x y d k $.
    $( The value of the Euclidean metric.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnmval $p |- ( ( N e. NN /\ X e. ( RR ^m ( 1 ... N ) )
                  /\ Y e. ( RR ^m ( 1 ... N ) ) ) -> ( X ( Rn ` N ) Y ) =
        ( sqr ` sum_ k e. ( 1 ... N ) ( ( ( X ` k ) - ( Y ` k ) ) ^ 2 ) ) ) $=
      ( vx vy cn wcel cr co cfv cv cmin c2 cexp csu csqr wceq wa fveq1 cfz cmap
      c1 crrn cmpt2 rrnval opreqd opreq12 syl2an opreq1d sumeq2sdv eqid ovmpt2a
      fveq2d fvex sylan9eq 3impb ) BGHZCIUCBUAJZUBJZHZDUTHZCDBUDKZJZUSALZCKZVED
      KZMJZNOJZAPZQKZRURVAVBSVDCDEFUTUTUSVEELZKZVEFLZKZMJZNOJZAPZQKZUEZJVKURVCV
      TCDEFABUFUGEFCDUTUTVSVKVTVLCRZVNDRZSZVRVJQWCUSVQVIAWCVPVHNOWAVMVFRVOVGRVP
      VHRWBVEVLCTVEVNDTVMVFVOVGMUHUIUJUKUNVTULVJQUOUMUPUQ $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.
    $( The underlying set of Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrndm $p |- ( N e. NN -> dom dom ( Rn ` N ) = ( RR ^m ( 1 ... N ) ) ) $=
      ( vx vy vk cn wcel crrn cfv cdm cr c1 cfz co cmap cxp cv cmin csqr syl6eq
      dmeqd c2 cexp csu cmpt2 rrnval eqid fvex dmmpt2 dmxpid ) AEFZAGHZIZIJKALM
      ZNMZUNOZIUNUJULUOUJULBCUNUNUMDPZBPHUPCPHQMUAUBMDUCZRHZUDZIUOUJUKUSBCDAUET
      BCUNUNURUSUSUFUQRUGUHSTUNUIS $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k z m n $.
    $( Euclidean space is a metric space.  (Contributed by Jeff Madsen,
       2-Sep-2009.)  (The proof was shortened by Mario Carneiro,
       5-Jun-2014.) $)
    rrnmet $p |- ( N e. NN -> ( Rn ` N ) e. Met ) $=
      ( vx vy vk wcel cfv cr wf co cc0 wceq wb caddc cle wral cexp csqr syl2anc
      wa c2 vz cn crrn cdm cxp cv wbr cme c1 cfz cmap cmin csu cmpt2 fzfid reex
      simprl oprex elmap sylib ffvelrn sylan simprr resubcl resqcl syl fsumrecl
      sqge0 fsumge0 resqrcl ralrimivva fmpt2 rrnval rrndm xpeq12d feq12d mpbird
      eqid eleq2d anbi12d biimpa sqr00 fsum00 recnd sqeq0 subeq0 bitrd ralbidva
      cc 3bitrd rrnmval 3expb eqeq1d wfn ffn eqfnfv2 3bitr4d adantr cfn adantlr
      simpr trirn npncan syl3anc opreq1d sumeq2dv fveq2d sqsubswap w3a 3adant3r
      3brtr3d 3adant3l opreq12d 3expa an32s 3brtr4d syldan ralrimiva fvex ismet
      jca cvv ax-mp sylanbrc ) AUBEZAUCFZUDUDZYGUEZGYFHZBUFZCUFZYFIZJKZYJYKKZLZ
      YLUAUFZYJYFIZYPYKYFIZMIZNUGZUAYGOZSZCYGOBYGOZYFUHEZYEYIGUIAUJIZUKIZUUFUEZ
      GBCUUFUUFUUEDUFZYJFZUUHYKFZULIZTPIZDUMZQFZUNZHZYEUUNGEZCUUFOBUUFOUUPYEUUQ
      BCUUFUUFYEYJUUFEZYKUUFEZSZSZUUMGEZJUUMNUGZUUQUVAUUEUULDUVAUIAUOZUVAUUHUUE
      EZSZUUKGEZUULGEUVFUUIGEZUUJGEZUVGUVAUUEGYJHZUVEUVHUVAUURUVJYEUURUUSUQGUUE
      YJUPUIAUJURZUSUTZUUEGUUHYJVAVBZUVAUUEGYKHZUVEUVIUVAUUSUVNYEUURUUSVCGUUEYK
      UPUVKUSUTZUUEGUUHYKVAVBZUUIUUJVDRZUUKVEVFZVGZUVAUUEUULDUVDUVRUVFUVGJUULNU
      GUVQUUKVHVFZVIZUUMVJRVKBCUUFUUFUUNGUUOUUOVRVLUTYEYHUUGGYFUUOBCDAVMYEYGUUF
      YGUUFAVNZUWBVOVPVQYEUUBBCYGYGYEYJYGEZYKYGEZSZUUTUUBYEUWEUUTYEUWCUURUWDUUS
      YEYGUUFYJUWBVSYEYGUUFYKUWBVSVTWAUVAYOUUAUVAUUNJKZUUIUUJKZDUUEOZYMYNUVAUWF
      UUMJKZUULJKZDUUEOUWHUVAUVBUVCUWFUWILUVSUWAUUMWBRUVAUUEUULDUVDUVRUVTWCUVAU
      WJUWGDUUEUVFUWJUUKJKZUWGUVFUUKWIEUWJUWKLUVFUUKUVQWDUUKWEVFUVFUUIWIEZUUJWI
      EZUWKUWGLUVFUUIUVMWDZUVFUUJUVPWDZUUIUUJWFRWGWHWJUVAYLUUNJYEUURUUSYLUUNKZD
      AYJYKWKWLZWMUVAYJUUEWNZYKUUEWNZYNUWHLUVAUVJUWRUVLUUEGYJWOVFUVAUVNUWSUVOUU
      EGYKWOVFDUUEYJYKWPRWQUVAYTUAYGUVAYPYGEZYPUUFEZYTUVAUWTUXAYEUWTUXALUUTYEYG
      UUFYPUWBVSWRWAUVAUXASZUUNUUEUUHYPFZUUIULITPIZDUMZQFZUUEUXCUUJULIZTPIDUMQF
      ZMIZYLYSNUXBUUEUUIUXCULIZUXGMIZTPIZDUMZQFUUEUXJTPIZDUMZQFZUXHMIUUNUXINUXB
      UUEUXJUXGDUVAUUEWSEUXAUVDWRUXBUVESZUVHUXCGEZUXJGEUVAUVEUVHUXAUVMWTUXBUUEG
      YPHZUVEUXRUXBUXAUXSUVAUXAXAGUUEYPUPUVKUSUTUUEGUUHYPVAVBZUUIUXCVDRUXQUXRUV
      IUXGGEUXTUVAUVEUVIUXAUVPWTUXCUUJVDRXBUXBUXMUUMQUXBUUEUXLUULDUXQUXKUUKTPUX
      QUWLUXCWIEZUWMUXKUUKKUVAUVEUWLUXAUWNWTZUXQUXCUXTWDZUVAUVEUWMUXAUWOWTUUIUX
      CUUJXCXDXEXFXGUXBUXPUXFUXHMUXBUXOUXEQUXBUUEUXNUXDDUXQUWLUYAUXNUXDKUYBUYCU
      UIUXCXHRXFXGXEXKUVAUWPUXAUWQWRYEUXAUUTYSUXIKZYEUXAUUTUYDYEUXAUUTXIYQUXFYR
      UXHMYEUXAUURYQUXFKUUSDAYPYJWKXJYEUXAUUSYRUXHKUURDAYPYKWKXLXMXNXOXPXQXRYAX
      QVKYFYBEUUDYIUUCSLAUCXSBCUAYBYFYGYGVRXTYCYD $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d k A $.  $d j k m n t x F $.  $d x J $.  $d j k n M $.  $d j k m n x N $.
    $d k n G $.  $d j k n x P $.  $d j k n x ph $.  $d k n R $.  $d k n X $.
    rrndstprj1.1 $e |- M = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    rrndstprj1.2 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    $( The distance between two points in Euclidean space is greater than the
       distance between the projections onto one coordinate.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj1 $p |- ( ( ( N e. NN /\ A e. ( 1 ... N ) ) /\ ( F e. X /\ G e. X
                    ) ) -> ( ( F ` A ) M ( G ` A ) ) <_ ( F ( Rn ` N ) G ) ) $=
      ( vk wcel co wa cfv c2 cexp cle wbr cr syl2anc syl cn c1 cfz cmin cabs cv
      csu csqr crrn fzfid wf cmap syl6eleq reex oprex elmap sylib ffvelrn sylan
      simprl simprr resubcl resqcl cc0 sqge0 wceq fveq2 opreq12d opreq1d simplr
      fsumge1 absresq fsumrecl fsumge0 resqrth 3brtr4d wb cc recnd abscl absge0
      resqrcl sqrge0 le2sq syl22anc mpbird eleq2i anbi12i rrnmval 3expb sylan2b
      remetdval adantlr ) EUAJZAUBEUCKZJZLZBFJZCFJZLZLZABMZACMZUDKZUEMZWOIUFZBM
      ZXFCMZUDKZNOKZIUGZUHMZXBXCDKZBCEUIMKZPXAXEXLPQZXENOKZXLNOKZPQZXAXDNOKZXKX
      PXQPXAWOXJXSIAXAUBEUJZXAXFWOJZLZXIRJZXJRJYBXGRJZXHRJZYCXAWORBUKZYAYDXABRW
      OULKZJZYFXABFYGWQWRWSUTHUMRWOBUNUBEUCUOZUPUQZWORXFBURUSXAWORCUKZYAYEXACYG
      JZYKXACFYGWQWRWSVAHUMRWOCUNYIUPUQZWORXFCURUSXGXHVBSZXIVCTZYBYCVDXJPQYNXIV
      ETZXFAVFZXIXDNOYQXGXBXHXCUDXFABVGXFACVGVHVIWNWPWTVJZVKXAXDRJZXPXSVFXAXBRJ
      ZXCRJZYSXAYFWPYTYJYRWORABURSZXAYKWPUUAYMYRWORACURSZXBXCVBSZXDVLTXAXKRJZVD
      XKPQZXQXKVFXAWOXJIXTYOVMZXAWOXJIXTYOYPVNZXKVOSVPXAXERJZVDXEPQZXLRJZVDXLPQ
      ZXOXRVQXAXDVRJZUUIXAXDUUDVSZXDVTTXAUUMUUJUUNXDWATXAUUEUUFUUKUUGUUHXKWBSXA
      UUEUUFUULUUGUUHXKWCSXEXLWDWEWFXAYTUUAXMXEVFUUBUUCXBXCDGWLSWNWTXNXLVFZWPWT
      WNYHYLLUUOWRYHWSYLFYGBHWGFYGCHWGWHWNYHYLUUOIEBCWIWJWKWMVP $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)

    $( Bound on the distance between two points in Euclidean space given bounds
       on the distances in each coordinate.  This theorem and ~ rrndstprj1 can
       be used to show that the supremum norm and Euclidean norm are
       equivalent.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj2 $p |- ( ( ( N e. NN /\ F e. X /\ G e. X ) /\ ( R e. RR+ /\
                    A. n e. ( 1 ... N ) ( ( F ` n ) M ( G ` n ) ) < R ) ) ->
                                ( F ( Rn ` N ) G ) < ( R x. ( sqr ` N ) ) ) $=
      ( vk wcel cfv co clt wbr c1 cr wceq syl syl2anc cn w3a crp cv cfz wral wa
      crrn cmin c2 cexp csu csqr cmul cmap simpl1 simpl2 simpl3 rrnmval syl3anc
      syl6eleq fzfid c0 wne nnuz eluzfz1 ne0i wf reex oprex elmap sylib ffvelrn
      cuz sylan resubcl resqcl simprl rpre adantr cabs absresq remetdval simprr
      fveq2 opreq12d breq1d rcla4cva eqbrtrrd cc0 wb cc recnd abscl rpge0 lt2sq
      cle absge0 syl22anc mpbid fsumlt fsumrecl sqge0 fsumge0 resqrth chash idi
      wi nnrp mulcom opreq2d cn0 nnnn0 hashfz1 opreq1d 3eqtr4d rpsqrcl rpcn cfn
      sqmul fsumconst 3brtr4d resqrcl sqrge0 rpmulcl mpbird eqbrtrd ) FUAKZCGKZ
      DGKZUBZAUCKZBUDZCLZYMDLZEMZANOZBPFUEMZUFZUGZUGZCDFUHLMZYRJUDZCLZUUCDLZUIM
      ZUJUKMZJULZUMLZAFUMLZUNMZNUUAYHCQYRUOMZKZDUULKZUUBUUIRYHYIYJYTUPZUUACGUUL
      YHYIYJYTUQIVAZUUADGUULYHYIYJYTURIVAZJFCDUSUTUUAUUIUUKNOZUUIUJUKMZUUKUJUKM
      ZNOZUUAUUHYRAUJUKMZJULZUUSUUTNUUAYRUUGUVBJUUAPFVBZUUAPYRKZYRVCVDUUAFPVNLZ
      KUVEUUAFUAUVFUUOVEVAPFVFSYRPVGSUUAUUCYRKZUGZUUFQKZUUGQKUVHUUDQKZUUEQKZUVI
      UUAYRQCVHZUVGUVJUUAUUMUVLUUPQYRCVIPFUEVJZVKVLYRQUUCCVMVOZUUAYRQDVHZUVGUVK
      UUAUUNUVOUUQQYRDVIUVMVKVLYRQUUCDVMVOZUUDUUEVPTZUUFVQSZUUAUVBQKZUVGUUAAQKZ
      UVSUUAYLUVTYKYLYSVRZAVSSZAVQSZVTUVHUUFWALZUJUKMZUUGUVBNUVHUVIUWEUUGRUVQUU
      FWBSUVHUWDANOZUWEUVBNOZUVHUUDUUEEMZUWDANUVHUVJUVKUWHUWDRUVNUVPUUDUUEEHWCT
      UUAYSUVGUWHANOZYKYLYSWDYQUWIBUUCYRYMUUCRZYPUWHANUWJYNUUDYOUUEEYMUUCCWEYMU
      UCDWEWFWGWHVOWIUVHUWDQKZWJUWDWQOZUVTWJAWQOZUWFUWGWKUVHUUFWLKZUWKUVHUUFUVQ
      WMZUUFWNSUVHUWNUWLUWOUUFWRSUUAUVTUVGUWBVTUUAUWMUVGUUAYLUWMUWAAWOSVTUWDAWP
      WSWTWIXAUUAUUHQKZWJUUHWQOZUUSUUHRUUAYRUUGJUVDUVRXBZUUAYRUUGJUVDUVRUVHUVIW
      JUUGWQOUVQUUFXCSXDZUUHXETUUAUVBUUJUJUKMZUNMZYRXFLZUVBUNMZUUTUVCUUAUVBFUNM
      ZFUVBUNMZUXAUXCUUAUVBWLKZFWLKUXDUXERUUAUVBUWCWMZUUAFUUAFUCKZFQKZUUAYHUXHU
      UAYHXHUUOXGFXISZFVSSZWMUVBFXJTUUAUWTFUVBUNUUAUXIWJFWQOZUWTFRUXKUUAUXHUXLU
      XJFWOSFXETXKUUAUXBFUVBUNUUAFXLKZUXBFRUUAYHUXMUUOFXMSFXNSXOXPUUAAWLKUUJWLK
      ZUUTUXARUUAAUWBWMUUAUUJUCKZUXNUUAUXHUXOUXJFXQSZUUJXRSAUUJXTTUUAYRXSKUXFUV
      CUXCRUVDUXGYRUVBJYATXPYBUUAUUIQKZWJUUIWQOZUUKQKZWJUUKWQOZUURUVAWKUUAUWPUW
      QUXQUWRUWSUUHYCTUUAUWPUWQUXRUWRUWSUUHYDTUUAUUKUCKZUXSUUAYLUXOUYAUWAUXPAUU
      JYETZUUKVSSUUAUYAUXTUYBUUKWOSUUIUUKWPWSYFYG $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)

    rrncms.3 $e |- J = ( MetOpen ` ( Rn ` N ) ) $.
    rrncms.4 $e |- ( ph -> N e. NN ) $.
    rrncms.5 $e |- ( ph -> F e. ( Cau ` ( Rn ` N ) ) ) $.
    rrncms.6 $e |- ( ph -> F : NN --> X ) $.
    rrncms.7 $e |- P = ( m e. ( 1 ... N ) |->
                         ( ~~> ` ( t e. NN |-> ( ( F ` t ) ` m ) ) ) ) $.
    $( Lemma for ~ rrncms . $)
    rrncmslem $p |- ( ph -> F e. dom ( ~~>t ` J ) ) $=
      ( vk vj wcel cn vx vn clm cfv wrel wbr cdm lmrel crrn cv co clt wral wrex
      cuz crp c1 eqid cme rrnmet syl cr cfz cmap wf wfn cmpt cli cvv fnmpt fvex
      a1i mprg wa nnuz cz wceq fveq2 fveq1d fvmpt adantl ffvelrn sylan syl6eleq
      1z reex oprex elmap sylib an32s eqeltrd recnd cmin cabs eqidd wi wb rrndm
      cca syl6eqr feq3 mpbird idi iscauf mpbid adantr ad3antrrr simpllr adantll
      uztrn2 syl2anc simplr rrndstprj1 syl22anc eleqtrrd metsym syl3anc breqtrd
      cle adantllr metcl ad2antrr ralimdva reximdva remetdval ralbidva rexbidva
      fveq2d breq1d sylibd ralrimiva sylibr adantlr rexuz3 ax-mp sylan2 anassrs
      mpd cc rpcn remet simpll remetba rpre lelttr mpand ad2antlr opreq12d nnex
      eqtr4d ralbidv mptex caucvg climdm simpl breqtrrd climrecl ffnfv sylanbrc
      mpteq2dva lmmbrf csqr cdiv id nnrp rpsqrcl rpdivcl syl2anr climi2 syl5bbr
      3syl cfn fzfi rexfiuz bitri cmul eleqtrd w3a rrndstprj2 expr syl31anc cc0
      wne rpne0 divcan1 breq2d mpbir2and releldm sylancr ) AFUCUDZUEECUWJUFZEUW
      JUGSFUHAUWKCHUIUDZUGUGZSQUJZEUDZCUWLUKZUAUJZULUFZQRUJZUOUDZUMZRTUNZUAUPUM
      AUAUWOUWLCRQEFUQUWMUWMTUWMURZLAHTSZUWLUSSZMHUTVAZACVBUQHVCUKZVDUKZUWMAUXG
      VBCVEZCUXHSACUXGVFZUBUJZCUDZVBSZUBUXGUMUXIUXJABTDUJZBUJZEUDZUDZVGZVHUDZVI
      SZUXJDUXGDUXGUXSCVIPVJUXTUXNUXGSUXRVHVKVLVMVLAUXMUBUXGAUXKUXGSZVNZUXLQBTU
      XKUXPUDZVGZUQVITVOUQVPSZUYBWEVLUYBUYDUYDVHUDZUXLVHUYBUYDVHUGSUYDUYFVHUFUY
      BUARQUYDUQVITVOUYBUWNTSZVNZUWNUYDUDZUYHUYIUXKUWOUDZVBUYGUYIUYJVQZUYBBUWNU
      YCUYJTUYDUXOUWNVQUXKUXPUWOUXOUWNEVRVSUYDURZUXKUWOVKVTZWAZAUYGUYAUYJVBSZAU
      YGVNZUXGVBUWOVEZUYAUYOUYPUWOUXHSUYQUYPUWOIUXHATIEVEZUYGUWOISZOTIUWNEWBZWC
      KWDVBUXGUWOWFUQHVCWGZWHWIUXGVBUXKUWOWBWCWJZWKZWLUYBUWSEUDZUWOUWLUKZUWQULU
      FZQUWTUMZRTUNZUAUPUMZUYIUWSUYDUDZWMUKZWNUDZUWQULUFZQUWTUMZRTUNZUAUPUMZAVU
      IUYAAEUWLWSUDSVUINAUAUWOVUDUWLRQEUQUWMTUXCVOUXFUYEAWEVLZUYPUWOWOZAUWSTSZV
      NZVUDWOATUWMEVEZWPAVVAUYROAUWMIVQZVVAUYRWQAUWMUXHIAUXDUWMUXHVQMHWRVAZKWTZ
      UWMITEXAVAXBZXCXDXEXFUYBVUIUYJUXKVUDUDZGUKZUWQULUFZQUWTUMZRTUNZUAUPUMVUPU
      YBVUHVVJUAUPUYBUWQUPSZVNZVUGVVIRTVVLVUSVNZVUFVVHQUWTVVMUWNUWTSZVNZVVGVUEX
      SUFZVUFVVHUYBVUSVVNVVPVVKUYBVUSVNZVVNVNZVVGUWOVUDUWLUKZVUEXSVVRUXDUYAUYSV
      UDISZVVGVVSXSUFAUXDUYAVUSVVNMXGAUYAVUSVVNXHVVRUYRUYGUYSAUYRUYAVUSVVNOXGZV
      USVVNUYGUYBUQUWNUWSTVOXJZXIZUYTXKZVVRUYRVUSVVTVWAUYBVUSVVNXLTIUWSEWBZXKZU
      XKUWOVUDGHIJKXMXNVVRUXEUWOUWMSZVUDUWMSZVVSVUEVQAUXEUYAVUSVVNUXFXGZVVRUWOI
      UWMVWDAVVBUYAVUSVVNVVDXGZXOZVVRVUDIUWMVWFVWJXOZUWOVUDUWLUWMUXCXPXQXRXTVVO
      VVGVBSZVUEVBSZUWQVBSZVVPVUFVNVVHWPUYBVUSVVNVWMVVKVVRGUSSZUYOVVFVBSZVWMVWP
      VVRGJUUAVLVVRUYBUYGUYOUYBVUSVVNUUBZVWCVUBXKZVVQVWQVVNAVUSUYAVWQVUTUXGVBVU
      DVEZUYAVWQVUTVUDUXHSVWTVUTVUDIUXHAUYRVUSVVTOVWEWCKWDVBUXGVUDWFVUAWHWIUXGV
      BUXKVUDWBWCWJXFZUYJVVFGVBGJUUCYAXQXTUYBVUSVVNVWNVVKVVRUXEVWHVWGVWNVWIVWLV
      WKVUDUWOUWLUWMUXCYAXQXTVVLVWOVUSVVNVVKVWOUYBUWQUUDWAYBVVGVUEUWQUUEXQUUFYC
      YDYCUYBVVJVUOUAUPUYBVVIVUNRTVVQVVHVUMQUWTVVRVVGVULUWQULVVRVVGUYJVVFWMUKZW
      NUDZVULVVRUYOVWQVVGVXCVQVWSVXAUYJVVFGJYEXKVVRVUKVXBWNVVRUYIUYJVUJVVFWMVVR
      UYBUYGUYKVWRVWCUYNXKVUSVUJVVFVQUYBVVNBUWSUYCVVFTUYDUXOUWSVQUXKUXPVUDUXOUW
      SEVRVSUYLUXKVUDVKVTUUGUUHYHUUJYIYFYGUUKYJYRUYDVISUYBBTUYCUUIUULVLUUMUYDUU
      NWIUYAUXLUYFVQADUXKUXSUYFUXGCUXNUXKVQZUXRUYDVHVXDBTUXQUYCVXDUXOTSZVNUXNUX
      KUXPVXDVXEUUOYHUUTYHPUYDVHVKVTWAUUPZVUCUXLVISZUYBUXKCVKZVLUUQZYKUBUXGVBCU
      URUUSVBUXGCWFVUAWHYLVVCXOZVOVUQVURVVEUVAVXJAUXBUAUPAVVKVNZUYJUXLGUKZUWQHU
      VBUDZUVCUKZULUFZUBUXGUMZQUWTUMZRTUNZUXBVXKVXOQUWTUMZRVPUNZUBUXGUMZVXRVXKV
      XTUBUXGVXKUYAVNZVXTUYJUXLWMUKWNUDZVXNULUFZQUWTUMZRTUNZVYBUXLUYJVXNRQUYDUQ
      VITVOUYEVYBWEVLVXKVXNUPSZUYAVVKVVKVXMUPSZVYGAVVKUVDAUXDHUPSVYHMHUVEHUVFUV
      KZUWQVXMUVGUVHZXFUYGUYKVYBUYMWAAUYAUYDUXLVHUFVVKVXFYMVXGVYBVXHVLUVIVXTVXS
      RTUNZVYBVYFUYEVYKVXTWQWEVXORQUQTVOYNYOVYBVXSVYERTVYBVUSVNVXOVYDQUWTVYBVUS
      VVNVXOVYDWQZVUSVVNVNZVYBUYGVYLVWBVYBUYGVNZVXLVYCVXNULVYNUYOUXMVXLVYCVQAUY
      AUYGUYOVVKVUBXTVYBUXMUYGAUYAUXMVVKVXIYMXFUYJUXLGJYEXKYIYPYQYFYGUVJXBYKVXR
      VXQRVPUNZVYAUYEVXRVYOWQWEVXPRQUQTVOYNYOUXGUVLSVYOVYAWQUQHUVMVXOUXGRQUBUVN
      YOUVOYLVXKVXQUXARTVXKVUSVNVXPUWRQUWTVXKVUSVVNVXPUWRWPZVYMVXKUYGVYPVWBVXKU
      YGVNZVXPUWPVXNVXMUVPUKZULUFZUWRVYQUXDUYSCISZVYGVXPVYSWPAUXDVVKUYGMYBVXKUY
      RUYGUYSAUYRVVKOXFUYTWCAVYTVVKUYGACUWMIVXJVVDUVQYBVXKVYGUYGVYJXFUXDUYSVYTU
      VRVYGVXPVYSVXNUBUWOCGHIJKUVSUVTUWAVYQVYRUWQUWPULVYQUWQYSSZVXMYSSZVXMUWBUW
      CZVYRUWQVQVYQVVKWUAAVVKUYGXLUWQYTVAVYQVYHWUBAVYHVVKUYGVYIYBZVXMYTVAVYQVYH
      WUCWUDVXMUWDVAUWQVXMUWEXQUWFYJYPYQYCYDYRYKUWGECUWJUWHUWI $.
      $( [6-Jun-2014] $)
  $}

  ${
    $d f j k m n t x N $.
    $( Euclidean space is complete.  (Contributed by Jeff Madsen, 2-Sep-2009.)
       (Revised by Mario Carneiro, 6-Jun-2014.) $)
    rrncms $p |- ( N e. NN -> ( Rn ` N ) e. CMet ) $=
      ( vf vt vm cn wcel crrn cfv cms cdm cv wf copn clm wa c1 co cmpt cr eqid
      wi cca wral cfz cli cabs cmin ccom cxp cres cmap simpll simplr wceq rrndm
      wb adantr feq3 syl biimpa rrncmslem ex ralrimiva nnuz cz 1z rrnmet iscms3
      a1i mpbird ) AEFZAGHZIFEVLJJZBKZLZVNVLMHZNHJFZUAZBVLUBHZUCVKVRBVSVKVNVSFZ
      OZVOVQWAVOOCDPAUDQZCEDKCKVNHHRUEHRZDVNVPUFUGUHSSUIUJZASWBUKQZWDTWETVPTZVK
      VTVOULVKVTVOUMWAVOEWEVNLZWAVMWEUNZVOWGUPVKWHVTAUOUQVMWEEVNURUSUTWCTVAVBVC
      VKVLBVPPVMEVDVMTWFPVEFVKVFVIAVGVHVJ $.
      $( [6-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d X a b d f m n r u v w x y z $.  $d M a b d f m n r u v w x y z $.
    $d Y a b d f m n r u v w x y z $.  $d N a b d f m n r u v w x y z $.
    rrntotbnd.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrntotbnd.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem1 $p |- ( ( ( ( N e. NN /\ x e. X ) /\ ( d e. RR+ /\ r e. RR+ )
          ) /\ ( y e. dom dom ( Rn ` N ) /\ ( x ( Rn ` N ) y ) < r ) ) ->
        ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
            ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\
            v = ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( ball ` ( Rn ` N ) ) d ) e.
        { a | E. f ( f : ( 1 ... N ) --> ( -u ( ( |_ ` ( r / ( d / ( sqr ` N )
            ) ) ) + 1 ) ... ( ( |_ ` ( r / ( d / ( sqr ` N ) ) ) ) + 1 ) ) /\
                a = ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
                                ( ( d / ( sqr ` N ) ) x. ( f ` m ) ) ) ) }
                                            ( ball ` ( Rn ` N ) ) d ) ) } ) $=
      ( wcel wa co cr cn cv crp crrn cfv cdm clt wbr c1 cfz csqr cdiv cfl caddc
      cneg wf cmin c2 wceq copab cmul cbl wex cab wral cabs cle cn0 simplr nnrp
      rpsqrcl syl rpdivcl sylan2 ancoms adantlr syl2anc cc0 rpre rpge0 flge0nn0
      nn0p1nn 3syl an31s ad2antrr wne cmap rrndm eleq2d reex oprex elmap syl6bb
      biimpa ffvelrn sylan adantllr eleq2i bitri adantll resubcl ad2ant2r rpne0
      cz sylanb redivcl syl3anc 2re 2ne0 rereccli readdcl sylancl flcl adantlrr
      zcn abscl recnd peano2zdi zre anasss peano2re absrdbnd absdiv absid eqtrd
      cc opreq2d cme adantr a1i wi eqbrtrd letrd wb sylib mpbid 3ad2ant1 eqeq2d
      eqid anbi2d recn rrnmet syl6reqr simpr metcl ad2antll ccom cres remetdval
      cxp remet remetba metsym biimprd imdistani rrndstprj1 ex anassrs eqbrtrrd
      an32s imp adantl ad2antlr ltle impr elrp lediv1 flwordi leadd1 nnre absle
      1re w3a syl2an biimp3a simp2 nnnegz mpbird ralrimiva fopab2 eqidd opabex2
      elfz feq1 fveq1 opabbidv opreq1d anbi12d cla4ev eqeq1 exbidv elab sylibr
      nnz ) IUAQZAUBZJQZRZNUBZUCQZLUBZUCQZRZRZBUBZIUDUEZUFUFZQZUWPUXEUXFSZUXAUG
      UHZRZRZUIIUJSZUXAUWSIUKUEZULSZULSZUMUEZUIUNSZUOZUXRUJSZEUBZUPZFUBZUXMQZDU
      BZUYCUWPUEZUXOUYCGUBZUXMQZCUBUYGUXEUEZUYGUWPUEZUQSZUXOULSZUIURULSZUNSZUMU
      EZUSRGCUTZUEZVASZUNSZUSZRZFDUTZUWSUXFVBUEZSZUYDUYEUYFUXOUYCUYAUEZVASZUNSZ
      USZRZFDUTZUWSVUCSZUSZRZEVCZVUDUYBMUBZVUKUSZRZEVCZMVDQUXLUXMUXTUYPUPZVUDVU
      DUSZVUNUXLUYOUXTQZGUXMVEVUSUXLVVAGUXMUXLUYHRZUXRUAQZUYOXDQZUYOVFUEZUXRVGU
      HZVVAUXDVVCUXKUYHUWOUXCVVCUWQUXCUWOVVCUWOUXBUWTVVCUWOUXBRUWTRZUXPUCQZUXQV
      HQZVVCVVGUXBUXOUCQZVVHUWOUXBUWTVIUWOUWTVVJUXBUWTUWOVVJUWOUWTUXNUCQZVVJUWO
      IUCQVVKIVJIVKVLUWSUXNVMVNZVOZVPUXAUXOVMZVQVVHUXPTQZVRUXPVGUHVVIUXPVSZUXPV
      TUXPWAVQUXQWBWCWDVOVPWEUXDUXHUYHVVDUXJUXDUXHRZUYHRZUYNTQZVVDVVRUYLTQZUYMT
      QVVSVVRUYKTQZUXOTQZUXOVRWFZVVTUWRUXHUYHVWAUXCUWRUXHRZUYHRZUYITQZUYJTQZVWA
      UWOUXHUYHVWFUWQUWOUXHRUXMTUXEUPZUYHVWFUWOUXHVWHUWOUXHUXETUXMWGSZQVWHUWOUX
      GVWIUXEIWHZWITUXMUXEWJUIIUJWKZWLWMWNUXMTUYGUXEWOWPWQZUWRUYHVWGUXHUWQUYHVW
      GUWOUWQUXMTUWPUPZUYHVWGUWQUWPVWIQVWMJVWIUWPOWRTUXMUWPWJVWKWLWSUXMTUYGUWPW
      OXEWTVPZUYIUYJXAVQWQZUXDVWBUXHUYHUWOUWTVWBUWQUXBUWTUWOVWBUWTUWORZVVJVWBVV
      LUXOVSVLVOZXBWEUXDVWCUXHUYHUWOUWTVWCUWQUXBUWTUWOVWCVWPVVJVWCVVLUXOXCVLVOX
      BZWEUYKUXOXFXGZURXHXIXJUYLUYMXKXLZUYNXMZVLXNVVBVVEUYLVFUEZUMUEZUIUNSZUXRV
      VBUYOYFQZVVETQVVBVVSVVDVXEUXDUXHUYHVVSUXJVWTXNVXAUYOXOWCUYOXPVLVVBVXBTQZV
      XDXDQVXDTQUXDUXHUYHVXFUXJVVRUYLYFQVXFVVRUYLVWSXQUYLXPVLXNZVXFVXCVXBXMZXRV
      XDXSWCVVBUXQTQZUXRTQZUXDVXIUXKUYHUWOUXCVXIUWQUWOUXCRVVOUXQXDQVXIUWOUWTUXB
      VVOUXBUWOUWTRZVVOUXBVXKRVVHVVOVXKUXBVVJVVHVVMVVNVNVVPVLVOXTZUXPXMUXQXSWCV
      PWEZUXQYAVLVVBVVTVVEVXDVGUHUXDUXHUYHVVTUXJVWSXNUYLYBVLVVBVXCUXQVGUHZVXDUX
      RVGUHZVVBVXFVVOVXBUXPVGUHVXNVXGUXDVVOUXKUYHUWOUXCVVOUWQVXLVPWEVVBVXBUYKVF
      UEZUXOULSZUXPVGVVBVXBVXPUXOVFUEZULSZVXQVVBUYKYFQZUXOYFQZVWCVXBVXSUSVVBUYK
      UXDUXHUYHVWAUXJVWOXNZXQUXDVYAUXKUYHUWOUWTVYAUWQUXBVXKUXOVWQXQXBWEUXDVWCUX
      KUYHVWRWEUYKUXOYCXGVVBVXRUXOVXPULUXDVXRUXOUSZUXKUYHUWOUWTVYCUWQUXBVXKVWBV
      RUXOVGUHZVYCVWQVXKVVJVYDVVMUXOVTVLUXOYDVQXBWEYGYEVVBVXPUXAVGUHZVXQUXPVGUH
      ZVVBVXPUXIUXAVVBVWAVXTVXPTQZVYBUYKUUAUYKXPWCZUXLUXITQZUYHUWRUXHVYIUXCUXJV
      WDUXFYHQZUWPUXGQZUXHVYIUWOVYJUWQUXHIUUBWEUWRVYKUXHUWOUWQVYKUWOJUXGUWPUWOU
      XGVWIJVWJOUUCZWIWNYIUWRUXHUUDUWPUXEUXFUXGUXGYSUUEXGZXBYIUXDUXATQZUXKUYHUX
      BVYNUWRUWTUXAVSZUUFWEZUWRUXKUYHVXPUXIVGUHZUXCUWRUXHUYHVYQUXJVWEUYIUYJVFUQ
      UUGTTUUJUUHZSZVXPUXIVGVWEVWFVWGVYSVXPUSVWLVWNUYIUYJVYRVYRYSZUUIVQVWEVYSUY
      JUYIVYRSZUXIVGVWEVYRYHQZVWFVWGVYSWUAUSWUBVWEVYRVYTUUKYJVWLVWNUYIUYJVYRTVY
      RVYTUULUUMXGVWDUWRUXEJQZRZUYHWUAUXIVGUHZUWRUXHWUCUWOUXHWUCYKUWQUWOWUCUXHU
      WOJUXGUXEVYLWIUUNYIUUOWUDUYHWUEUWOUWQWUCUYHWUEYKUWOUWQWUCRZRUYHWUEUWOUYHW
      UFWUEUYGUWPUXEVYRIJVYTOUUPUUTUUQUURUVAWPYLUUSXNWQUXLUXIUXAVGUHZUYHUXDUXHU
      XJWUGVVQVYIVYNUXJWUGYKUWRUXHVYIUXCVYMVPUXCVYNUWRUXHUXBVYNUWTVYOUVBUVCUXIU
      XAUVDVQUVEYIYMVVBVYGVYNVWBVRUXOUGUHRZVYEVYFYNVYHVYPVVBVVJWUHUXDVVJUXKUYHU
      WOUWTVVJUWQUXBVVMXBWEUXOUVFYOVXPUXAUXOUVGXGYPYLVXBUXPUVHXGVVBVXCTQZVXIUIT
      QZVXNVXOYNVVBVXFVXCXDQWUIVXGVXHVXCXSWCVXMWUJVVBUVLYJVXCUXQUIUVIXGYPYMVVCV
      VDVVFUVMZVVAUXSUYOVGUHUYOUXRVGUHRZVVCVVDVVFWULVVDVVCVVFWULYNZVVDUYOTQVXJW
      UMVVCUYOXSUXRUVJUYOUXRUVKUVNVOUVOWUKVVDUXSXDQZUXRXDQZVVAWULYNVVCVVDVVFUVP
      VVCVVDWUNVVFUXRUVQYQVVCVVDWUOVVFUXRUWNYQUYOUXSUXRUWCXGUVRXGUVSGCUXMUXTUYO
      UYPUYPYSUVTYOUXLVUDUWAVUMVUSVUTREUYPGCUXMUYOVWKUWBUYAUYPUSZUYBVUSVULVUTUX
      MUXTUYAUYPUWDWUPVUKVUDVUDWUPVUJVUBUWSVUCWUPVUIVUAFDWUPVUHUYTUYDWUPVUGUYSU
      YEWUPVUFUYRUYFUNWUPVUEUYQUXOVAUYCUYAUYPUWEYGYGYRYTUWFUWGYRUWHUWIVQVURVUNM
      VUDVUBUWSVUCWKVUOVUDUSZVUQVUMEWUQVUPVULUYBVUOVUDVUKUWJYTUWKUWLUWM $.
      $( [2-Sep-2009] $)

    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem2 $p |- ( ( ( N e. NN /\ d e. RR+ ) /\
          ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) ) ->
                  ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
        ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\ v =
                ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                      ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( Rn ` N ) y ) < d ) $=
      ( wcel wa cr co cfv wceq cmul va cn cv crp c1 cfz cmap csqr cdiv c2 caddc
      cmin cfl copab crrn clt cabs ccom cxp cres wbr wral simpll wf oprex elmap
      reex ffvelrn sylanb adantll adantlrr nnrp rpsqrcl syl rpdivcl sylan2 rpre
      ancoms ad2antrr cc0 wne adantlr resubcl syl2anc rpne0 redivcl syl3anc 2re
      cz 2ne0 rereccli readdcl sylancl flcl zre 3syl ralrimiva eqid sylib sylan
      fopab2 remulcl sylibr simprr adantr fveq2 opreq2d opreq12d fvopab4 adantl
      opreq1d fveq2d fvex jca simprl remetdval cc recnd subcl abscl a1i 1re cle
      rddif halflt1 lelttrd rpgt0 ltmul2 syl112anc mpbid recn ad2antrl ad2antll
      wb addcom subsub3 rpcn divcan2 eqcomd eqbrtrd subdi eqtr4d 3eqtr2d absmul
      rpge0 absid 3eqtrd mulid1 3brtr4d syl2an anassrs syl32anc divcan1 breqtrd
      rrndstprj2 ) HUBNZKUCZUDNZOZAUCZPUEHUFQZUGQZNZBUCZUVBNZOZOZEUCZUVANZDUCUV
      HUUTRZUUQHUHRZUIQZUVHFUCZUVANZCUCUVMUVDRZUVMUUTRZULQZUVLUIQZUEUJUIQZUKQZU
      MRZSOFCUNZRZTQZUKQZSOEDUNZUVDHUORQZUVLUVKTQZUUQUPUVGUUPUWFUVBNZUVEUVLUDNZ
      UAUCZUWFRZUWKUVDRZUQULURPPUSUTZQZUVLUPVAZUAUVAVBUWGUWHUPVAUUPUURUVFVCUVGU
      VAPUWFVDZUWIUVGUWEPNZEUVAVBUWQUVGUWREUVAUVGUVIOZUVJPNZUWDPNZUWRUUSUVCUVIU
      WTUVEUVCUVIUWTUUSUVCUVAPUUTVDZUVIUWTPUVAUUTVGUEHUFVEZVFZUVAPUVHUUTVHVIVJV
      KUWSUVLPNZUWCPNZUXAUUSUXEUVFUVIUURUUPUXEUURUUPOZUWJUXEUUPUURUVKUDNZUWJUUP
      HUDNZUXHHVLZHVMZVNUUQUVKVOVPZUVLVQZVNVRZVSUVGUVAPUWBVDZUVIUXFUVGUWAPNZFUV
      AVBUXOUVGUXPFUVAUVGUVNOZUVTPNZUWAWINUXPUXQUVRPNZUVSPNZUXRUXQUVQPNZUXEUVLV
      TWAZUXSUVFUVNUYAUUSUVFUVNOUVOPNZUVPPNZUYAUVEUVNUYCUVCUVEUVAPUVDVDZUVNUYCP
      UVAUVDVGUXCVFZUVAPUVMUVDVHVIVJUVCUVNUYDUVEUVCUXBUVNUYDUXDUVAPUVMUUTVHVIWB
      UVOUVPWCWDVJUUSUXEUVFUVNUXNVSUUSUYBUVFUVNUURUUPUYBUXGUWJUYBUXLUVLWEZVNVRV
      SUVQUVLWFWGUJWHWJWKZUVRUVSWLWMUVTWNUWAWOWPWQFCUVAPUWAUWBUWBWRZXAWSUVAPUVH
      UWBVHWTUVLUWCXBWDUVJUWDWLWDWQEDUVAPUWEUWFUWFWRZXAWSPUVAUWFVGUXCVFXCUUSUVC
      UVEXDUUSUWJUVFUURUUPUWJUXLVRZXEUVGUWPUAUVAUVGUWKUVANZOZUWOUWKUUTRZUVLUWKU
      WBRZTQZUKQZUWMUWNQZUVLUPUYLUWOUYRSUVGUYLUWLUYQUWMUWNEDUWKUWEUYQUVAUWFUVHU
      WKSZUVJUYNUWDUYPUKUVHUWKUUTXFUYSUWCUYOUVLTUVHUWKUWBXFXGXHUYJUYNUYPUKVEXIX
      KXJUYMUYRUYNUVLUWMUYNULQZUVLUIQZUVSUKQZUMRZTQZUKQZUWMUWNQZUVLUPUYLUYRVUFS
      UVGUYLUYQVUEUWMUWNUYLUYPVUDUYNUKUYLUYOVUCUVLTFCUWKUWAVUCUVAUWBUVMUWKSZUVT
      VUBUMVUGUVRVUAUVSUKVUGUVQUYTUVLUIVUGUVOUWMUVPUYNULUVMUWKUVDXFUVMUWKUUTXFX
      HXKXKXLUYIVUBUMXMXIXGXGXKXJUUSUVFUYLVUFUVLUPVAZUUSUWJUYNPNZUWMPNZOZVUHUVF
      UYLOZUYKVULVUIVUJUVCUYLVUIUVEUVCUXBUYLVUIUXDUVAPUWKUUTVHVIWBUVEUYLVUJUVCU
      VEUYEUYLVUJUYFUVAPUWKUVDVHVIVJXNUWJVUKOZVUFVUEUWMULQZUQRZUVLUPVUMVUEPNZVU
      JVUFVUOSVUMVUIVUDPNZVUPUWJVUIVUJXOVUMUXEVUCPNZVUQUWJUXEVUKUXMXEZVUMVUBPNZ
      VUCWINVURVUMVUAPNZUXTVUTVUMUYTPNZUXEUYBVVAVUKVVBUWJVUJVUIVVBUWMUYNWCVRXJZ
      VUSUWJUYBVUKUYGXEZUYTUVLWFWGZUYHVUAUVSWLWMVUBWNVUCWOWPZUVLVUCXBWDZUYNVUDW
      LWDUWJVUIVUJXDVUEUWMUWNUWNWRZXPWDVUMUVLVUCVUAULQZUQRZTQZUVLUETQZVUOUVLUPV
      UMVVJUEUPVAZVVKVVLUPVAZVUMVVJUVSUEVUMVVIXQNZVVJPNZVUMVUCXQNZVUAXQNZVVOVUM
      VUCVVFXRZVUMVUAVVEXRZVUCVUAXSWDZVVIXTVNZUXTVUMUYHYAUEPNZVUMYBYAZVUMVVAVVJ
      UVSYCVAVVEVUAYDVNUVSUEUPVAVUMYEYAYFVUMVVPVWCUXEVTUVLUPVAZVVMVVNYNVWBVWDVU
      SUWJVWEVUKUVLYGXEVVJUEUVLYHYIYJVUMVUOUVLVVITQZUQRZUVLUQRZVVJTQZVVKVUMVUNV
      WFUQVUMVUNVUDUYNUKQZUWMULQZVUDUYTULQZVWFVUMVUEVWJUWMULVUMUYNXQNZVUDXQNZVU
      EVWJSVUIVWMUWJVUJUYNYKYLZVUMVUDVVGXRZUYNVUDYOWDXKVUMVWNUWMXQNZVWMVWLVWKSV
      WPVUJVWQUWJVUIUWMYKYMVWOVUDUWMUYNYPWGVUMVWLVUDUVLVUATQZULQZVWFVUMUYTVWRVU
      DULVUMVWRUYTVUMUYTXQNUVLXQNZUYBVWRUYTSVUMUYTVVCXRUWJVWTVUKUVLYQZXEZVVDUYT
      UVLYRWGYSXGVUMVWTVVQVVRVWFVWSSVXBVVSVVTUVLVUCVUAUUAWGUUBUUCXLVUMVWTVVOVWG
      VWISVXBVWAUVLVVIUUDWDVUMVWHUVLVVJTVUMUXEVTUVLYCVAZVWHUVLSVUSUWJVXCVUKUVLU
      UEXEUVLUUFWDXKUUGVUMVVLUVLUWJVVLUVLSZVUKUWJVWTVXDVXAUVLUUHVNXEYSUUIYTUUJU
      UKYTYTWQUVLUAUWFUVDUWNHUVBVVHUVBWRUUOUULUUSUWHUUQSZUVFUUSUUQXQNZUVKXQNZUV
      KVTWAZVXEUURVXFUUPUUQYQXJUUPVXGUURUUPUXIUXHVXGUXJUXKUVKYQWPXEUUPVXHUURUUP
      UXIUXHVXHUXJUXKUVKWEWPXEUUQUVKUUMWGXEUUN $.
      $( [2-Sep-2009] $)

    $( A set in Euclidean space is totally bounded iff its is bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrntotbnd $p |- ( ( N e. NN /\ Y C_ X ) ->
                                      ( M e. TotBnd <-> M e. Bnd ) ) $=
      ( vx vb vf vm wcel wss wa cv cfv co wceq cfn cr c0 vr vv vy vd va ctotbnd
      vu vz vn cbnd cdm eqid totbndbnd cbl crp wrex wral cuni crrn cxp cres a1i
      cn dmeqd cme rrnmet adantr cfz cmap rrndm syl6eqr sseq2d biimpar metssba2
      c1 syl2anc eqtr4d raleqdv wi 0fin id eqsstrd adantl unieq anbi12d rcla4ev
      0ss raleq ralrimiva wne cdiv cfl caddc wf cmul wex cab anim2i anassrs clt
      copab wbr wb ad2antrr eleq2d simprr syl3anc c2 biimpi oprex elmap ffvelrn
      reex sylanb adantll adantlrr sylan2 ancoms cz adantlr readdcl 3syl fopab2
      sylib sylan remulcl syl6bb mpbird ad2ant2rl an32s adantrr ex sylbid bitri
      syl sstri fzfi mp2an cvv ax-mp ral0 jctir sylancr a1d wn r19.2z csqr cneg
      df-ne ssel2 anim1i anasss biimpa elbl cmin rrntotbndlem1 biimpd imdistani
      syl6reqr eleq2i rrntotbndlem2 nnrp rpsqrcl rpdivcl rpre cc0 resubcl rpne0
      simplr redivcl 2re 2ne0 rereccli sylancl flcl zre elbl2 syl22anc expr imp
      sylanl1 eleq2 eluni2 sylibr ssrdv eqeq1d jca blssp inss1 sseq1 syl5ibrcom
      an4s cin sylbird sstr expcom sylsyld vex eqeq1 anbi2d exbidv fzssuz uzssz
      elab cuz zssre syl2an opreq1 eqeq2d exlimdv syl5bi ralrimiv jctird df-rex
      fss mpan2 anbi1i exbii abbii xpfi pwfi mpbi mapsspw ssfi abrexfi eqeltrri
      cpw mpan syl6 ralrimdva rexlimdvv syl5 sylan2br pm2.61dan metres syl5eqel
      expdimp isbnd2 sstotbnd 3imtr4d impbid2 ) BVCKZDCLZMZAUFKZAUJKZAAUKZUKZVU
      HULZUMVUDVUHGNZUANZAUNOPZQZUAUOUPZGVUHUQZDUBNZURZLZHNZUCNZUDNZBUSOZUNOZPZ
      QZUCVVBUKUKZUPZHVUPUQZMZUBRUPZUDUOUQZVUFVUEVUDVUOVUNGDUQZVVKVUDVUNGVUHDVU
      DVUHVVBDDUTVAZUKZUKZDVUDVUGVVNVUDAVVMAVVMQVUDFVBVDVDZVUDVVBVEKZDVVFLZDVVO
      QVUBVVQVUCBVFZVGZVUBVVRVUCVUBVVFCDVUBVVFSVOBVHPZVIPZCBVJZEVKVLVMZVVBVVFDV
      VFULZVNVPZVQVRVUDDTQZVVLVVKVSZVUDVWGMZVVKVVLVWIVVJUDUOVWIVVJVVAUOKZVWITRK
      DTURZLZVVGHTUQZMZVVJVTVWIVWLVWMVWGVWLVUDVWGDTVWKVWGWATVWKLVWGVWKWGVBWBWCV
      VGHUUAUUBVVIVWNUBTRVUPTQZVURVWLVVHVWMVWOVUQVWKDVUPTWDVLVVGHVUPTWHWEWFUUCV
      GWIUUDVWGUUEVUDDTWJZVWHDTUUIVUDVWPVVLVVKVWPVVLMVUNGDUPVUDVVKVUNGDUUFVUDVU
      MVVKGUADUOVUDVUJDKZVUKUOKZMZVUMVVKVSVUDVWSMZVUMVVJUDUOVWTVWJMZVUMDVWAVUKV
      VABUUGOZWKPZWKPWLOVOWMPZUUHZVXDVHPZINZWNZUENZJNZVWAKZUGNZVXJVUJOZVXCVXJVX
      GOZWOPZWMPZQMJUGXAZVVAVVCPZQZMZIWPZUEWQZURZLZVVGHVYBUQZMZVVJVXAVUMVYDVYEV
      XAVUJVUKVVCPZVYCLZVUMDVYGLZVYDVXAUCVYGVYCVWTVUBVUJCKZVWRMZMZVWJVUTVYGKZVU
      TVYCKZVSZVUBVUCVWSVYLVUCVWSMVYKVUBVUCVWQVWRVYKVUCVWQMZVYJVWRDCVUJUUJZUUKU
      ULWRWSVUBVWJVYKVYOVUBVYJVWJVWRVYOVUBVYJMZVWJVWRMZMZVYMVUTVVFKZVUJVUTVVBPV
      UKWTXBZMZVYNVYTVVQVUJVVFKZVWRVYMWUCXCVUBVVQVYJVYSVVSXDVYRWUDVYSVUBVYJWUDV
      UBCVVFVUJVUBVVFVWBCVWCEUUSXEUUMVGVYRVWJVWRXFVUTVVBVUJVUKVVFVWEUUNXGVYTWUC
      VYNVYTWUCMZVUTUHNZKZUHVYBUPZVYNWUEVXKVXLVXMVXCVXJUINZVWAKZVUPWUIVUTOZWUIV
      UJOZUUOPZVXCWKPZVOXHWKPZWMPZWLOZQMUIUBXAZOZWOPZWMPZQMJUGXAZVVAVVCPZVYBKVU
      TWVCKZWUHGUCUBUGIJUIABCDUAUEUDEFUUPVYTWUAWVDWUBVYRVWJWUAWVDVWRVYRVWJMZWUA
      MWVEVUTVWBKZMWVDWVEWUAWVFVUBWUAWVFVSVYJVWJVUBWUAWVFVUBVVFVWBVUTVWCXEZUUQX
      DUURVYRVUBVUJVWBKZMZVWJWVFWVDVYJWVHVUBVYJWVHCVWBVUJEUUTZXIWRWVIVWJMWVFWVD
      VUBVWJWVHWVFWVDVSVUBVWJMZWVHWVFWVDWVKWVHWVFMZMZWVDWVBVUTVVBPVVAWTXBZGUCUB
      UGJUIABCDUDEFUVAWVMVVQVWJWVBVVFKZWUAWVDWVNXCVUBVVQVWJWVLVVSXDVUBVWJWVLUVI
      WVMWVOVWASWVBWNZWVMWVASKZJVWAUQWVPWVMWVQJVWAWVMVXKMZVXMSKZWUTSKZWVQWVKWVH
      VXKWVSWVFWVHVXKWVSWVKWVHVWASVUJWNZVXKWVSSVWAVUJXMVOBVHXJZXKZVWASVXJVUJXLZ
      XNXOXPWVRVXCSKZWUSSKZWVTWVKWWEWVLVXKVWJVUBWWEVWJVUBMZVXCUOKZWWEVUBVWJVXBU
      OKZWWHVUBBUOKWWIBUVBBUVCYOVVAVXBUVDXQZVXCUVEYOXRZXDWVMVWASWURWNZVXKWWFWVM
      WUQSKZUIVWAUQWWLWVMWWMUIVWAWVMWUJMZWUPSKZWUQXSKWWMWWNWUNSKZWUOSKWWOWWNWUM
      SKZWWEVXCUVFWJZWWPWVLWUJWWQWVKWVLWUJMWUKSKZWULSKZWWQWVFWUJWWSWVHWVFVWASVU
      TWNWUJWWSSVWAVUTXMWWBXKVWASWUIVUTXLXNXOWVHWUJWWTWVFWVHWWAWUJWWTWWCVWASWUI
      VUJXLXNXTWUKWULUVGVPXOWVKWWEWVLWUJWWKXDWVKWWRWVLWUJVWJVUBWWRWWGWWHWWRWWJV
      XCUVHYOXRXDWUMVXCUVJXGXHUVKUVLUVMWUNWUOYAUVNWUPUVOWUQUVPYBWIUIUBVWASWUQWU
      RWURULYCYDVWASVXJWURXLYEVXCWUSYFVPVXMWUTYAVPWIJUGVWASWVAWVBWVBULYCYDWVMWV
      OWVBVWBKWVPWVMVVFVWBWVBVUBVVFVWBQVWJWVLVWCXDXESVWAWVBXMWWBXKYGYHVUBWVFWUA
      VWJWVHVUBWUAWVFWVGVMYIVUTVVBWVBVVAVVFVWEUVQUVRYHUVSYJUVTUWAYOXPYKWUGWVDUH
      WVCVYBWUFWVCVUTUWBWFVPUHVUTVYBUWCUWDYLYMUWLYJYEUWEVWTVUMVYIVSVWJVWTVUMDVU
      LQZVYIVWTDVUHVULVUDDVUHQVWSVUDDVVOVUHVWFVVPVQVGUWFVWTVYIWXAVULVYGLZVUDVVQ
      VVRMZVWSWXBVUDVVQVVRVVTVWDUWGWXCVWSMZVULVYGDUWMZVYGVUKDVVBAVVFVUJVWEFUWHW
      XEVYGLWXDVYGDUWIVBWBYEDVULVYGUWJUWKUWNVGVYIVYHVYDDVYGVYCUWOUWPUWQVXAVVGHV
      YBVUSVYBKVXHVUSVXRQZMZIWPZVXAVVGVYAWXHUEVUSHUWRVXIVUSQZVXTWXGIWXIVXSWXFVX
      HVXIVUSVXRUWSUWTUXAUXDVXAWXGVVGIVXAWXGVVGVXAWXGMVXQVVFKZWXFVVGVXAVXHWXJWX
      FVXAVXHMZWXJVWASVXQWNZWXKVXPSKZJVWAUQWXLWXKWXMJVWAVXAVXHVXKWXMVXAVXHVXKMZ
      MWVSVXOSKZWXMVWTVXKWVSVWJVXHVUDVWQVXKWVSVWRVUDVWQMWWAVXKWVSVUBVUCVWQWWAVY
      PVUBVYJWWAVYQVYJWWAVUBVYJWWAVYJWVHWWAWVJWWCYNXIWCXQWSWWDYEXPYIVXAWWEVXNSK
      ZWXOWXNVUDVWJWWEVWSVUBVWJWWEVUCWWKXTXTVXHVWASVXGWNZVXKWXPVXHVXFSLWXQVXFXS
      SVXFVXEUXEOXSVXEVXDUXBVXEUXCYPUXFYPVWAVXFSVXGUXOUXPVWASVXJVXGXLYEVXCVXNYF
      UXGVXMVXOYAVPWSWIJUGVWASVXPVXQVXQULYCYDVWTWXJWXLXCZVWJVXHVUBWXRVUCVWSVUBW
      XJVXQVWBKWXLVUBVVFVWBVXQVWCXESVWAVXQXMWWBXKYGXDXDYHYKVXAVXHWXFXFVVEWXFUCV
      XQVVFVUTVXQQVVDVXRVUSVUTVXQVVAVVCUXHUXIWFVPYLUXJUXKUXLUXMVYBRKVYFVVJVXSIV
      XFVWAVIPZUPZUEWQZVYBRWXTVYAUEWXTVXGWXSKZVXSMZIWPVYAVXSIWXSUXNWYCVXTIWYBVX
      HVXSVXFVWAVXGVXEVXDVHXJWWBXKUXQUXRYNUXSWXSRKZWYARKVWAVXFUTZUYGZRKZWXSWYFL
      ZWYDWYERKZWYGVWARKVXFRKWYIVOBYQVXEVXDYQVWAVXFUXTYRWYEUYAUYBVWAYSKWYHWWBVX
      FVWAYSUYCYTWYFWXSUYDYRIUEWXSVXRUYEYTUYFVVIVYFUBVYBRVUPVYBQZVURVYDVVHVYEWY
      JVUQVYCDVUPVYBWDVLVVGHVUPVYBWHWEWFUYHUYIUYJYLUYKUYLUYQUYMUYNYMVUDVVQAVEKV
      UFVUOXCVVTVVQAVVMVEFVVBDUYOUYPGAVUHUAVUIUYRYBVUDVVQVVRVUEVVKXCVVTVWDUCUBV
      VBAVVFDHUDVWEFUYSVPUYTVUA $.
      $( [28-Feb-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    rrnheibor.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrnheibor.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    rrnheibor.3 $e |- T = ( MetOpen ` M ) $.
    rrnheibor.4 $e |- U = ( MetOpen ` ( Rn ` N ) ) $.
    $( Heine-Borel theorem for Euclidean space.  A subset of Euclidean space is
       compact iff it is closed and bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnheibor $p |- ( ( N e. NN /\ Y C_ X ) ->
                      ( T e. Comp <-> ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( wcel wss wa cms cfv cme adantr cdm eqid co ccomp ctotbnd ccld cbnd crrn
      rrnmet cxp cres metres syl5eqel syl biantrurd heibor syl6bb eleq1i rrncms
      cn cr c1 cmap rrndm syl6reqr sseq2d biimpa cmsss syl5bb rrntotbnd anbi12d
      cfz bitrd ) DUQKZFELZMZAUAKZCNKZCUBKZMZFBUCOKZCUDKZMVMVNCPKZVNMVQVMVTVNVK
      VTVLVKDUEOZPKZVTDUFWBCWAFFUGUHZPHWAFUIUJUKQULCACRRZIWDSUMUNVMVOVRVPVSVOWC
      NKVMVRCWCNHUOVMWABWARRZFWESJVKWANKVLDUPQVKVLFWELVKEWEFVKWEURUSDVITUTTEDVA
      GVBVCVDVEVFCDEFGHVGVHVJ $.
      $( [2-Sep-2009] $)
  $}

