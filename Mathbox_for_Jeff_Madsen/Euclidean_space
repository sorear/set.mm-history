$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Euclidean space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The real numbers are a complete metric space.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    recms $p |- ( ( abs o. - ) |` ( RR X. RR ) ) e. CMet $=
      ( vx vy cmin cr wcel cfv cc wss co wa wb cc0 3syl wceq syl wbr cle adantr
      c2 cexp cabs ccom cxp cres cms copn ccld cdif cbl crp wrex wral cme cnmet
      cv eqid cnmetba isopn4 ax-mp difss cim wne eldifi imcl recn eldifn reim0b
      wn necon3bbid mpbid absrpcl syl11anc a1i blssm syl111anc sseld elbl caddc
      clt subcl syl2an recl sqge0 resqcl addge02 absresq absvalsq2 imsub adantl
      cre reim0 opreq2d subid1 3eqtrd opreq1d eqtrd abscl absge0 le2sq syl22anc
      3brtr4d mpbird cnmetdval breq2d metcl lenlt bitr3d ex adantld sylbid jcad
      con2d eldif syl6ibr ssrdv opreq2 sseq1d rcla4ev rgen mpbir2an ctop opntop
      ax-resscn cuni uniopn2 iscld2 mp2an mpbir cncms cmsss ) UACUBZDDUCUDUEEZD
      YKUFFZUGFEZYNGDUHZYMEZYPYOGHZAUOZBUOZYKUIFZIZYOHZBUJUKZAYOULZYKUMEZYPYQUU
      DJKYKYKUPZUNZABYOYKYMGYKUUFUQZYMUPZURUSGDUTUUCAYOYRYOEZYRVAFZUAFZUJEZYRUU
      LYTIZYOHZUUCUUJUUKGEZUUKLVBZUUMUUJYRGEZUUKDEZUUPYRGDVCZYRVDZUUKVEZMZUUJYR
      DEZVHUUQYRGDVFUUJUVDUUKLUUJUURUVDUUKLNKUUTYRVGOVIVJUUKVKVLZUUJBUUNYOUUJYS
      UUNEZYSGEZYSDEZVHZJYSYOEUUJUVFUVGUVIUUJUUNGYSUUJUUEUURUUMUUNGHUUEUUJUUGVM
      ZUUTUVEYKYRUULGUUHVNVOVPUUJUVFUVGYRYSYKIZUULVSPZJZUVIUUJUUEUURUUMUVFUVMKU
      VJUUTUVEYSYKYRUULGUUHVQVOUUJUVLUVIUVGUUJUVHUVLUUJUVHUVLVHZUUJUVHJZUULYRYS
      CIZUAFZQPZUVNUVOUVRUULSTIZUVQSTIZQPZUVOUUKSTIZUVPWJFZSTIZUWBVRIZUVSUVTQUV
      OLUWDQPZUWBUWEQPZUVOUVPGEZUWCDEZUWFUUJUURUVGUWHUVHUUTYSVEZYRYSVTWAZUVPWBZ
      UWCWCMUVOUWBDEZUWDDEZUWFUWGKUVOUUSUWMUUJUUSUVHUUJUURUUSUUTUVAORZUUKWDOUVO
      UWHUWIUWNUWKUWLUWCWDMUWBUWDWEVLVJUVOUUSUVSUWBNUWOUUKWFOUVOUVTUWDUVPVAFZST
      IZVRIZUWEUVOUWHUVTUWRNUWKUVPWGOUVOUWQUWBUWDVRUVOUWPUUKSTUVOUWPUUKYSVAFZCI
      ZUUKLCIZUUKUUJUURUVGUWPUWTNUVHUUTUWJYRYSWHWAUVOUWSLUUKCUVHUWSLNUUJYSWKWIW
      LUVOUUPUXAUUKNUUJUUPUVHUVCRUUKWMOWNWOWLWPXAUVOUULDEZLUULQPZUVQDEZLUVQQPZU
      VRUWAKUUJUXBUVHUUJUURUUSUXBUUTUVAUUSUUPUXBUVBUUKWQOMRZUUJUXCUVHUUJUUPUXCU
      VCUUKWRORUVOUWHUXDUWKUVPWQOUVOUWHUXEUWKUVPWROUULUVQWSWTXBUVOUULUVKQPZUVRU
      VNUVOUVKUVQUULQUUJUURUVGUVKUVQNUVHUUTUWJYRYSYKUUFXCWAXDUVOUXBUVKDEZUXGUVN
      KUXFUVOUUEUURUVGUXHUUEUVOUUGVMUUJUURUVHUUTRUVHUVGUUJUWJWIYRYSYKGUUHXEVOUU
      LUVKXFVLXGVJXHXLXIXJXKYSGDXMXNXOUUBUUOBUULUJYSUULNUUAUUNYOYSUULYRYTXPXQXR
      VLXSXTYMYAEZDGHZYNYPKUUEUXIUUGYKYMUUIYBUSYCDYMGUUEGYMYDNUUGYKYMGUUHUUIYEU
      SYFYGYHYKUEEUXJYLYNKYKUUFYIYCYKYMGDUUHUUIYJYGYH $.
      $( [2-Sep-2009] $)
  $}

  $c Rn $.
  $( Extend class notation with the n-dimensional Euclidean space. $)
  crrn $a class Rn $.

  ${
    $d n r x y d k $.
    $( Define n-dimensional Euclidean space as a metric space with the standard
       Euclidean norm given by the quadratic mean. $)
    df-rrn $a |- Rn = { <. n , r >. | ( n e. NN /\ r = { <. <. x , y >. , d >.
          | ( ( x e. ( RR ^m ( 1 ... n ) ) /\ y e. ( RR ^m ( 1 ... n ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... n )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) } $.
  $}

  ${
    $d N n r x y d k $.
    $( The n-dimensional Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnval $p |- ( N e. NN -> ( Rn ` N ) = { <. <. x , y >. , d >. |
            ( ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... N )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) $=
      ( vn vr cv cr c1 cfz co cmap wcel wa cfv csu csqr wceq copab2 cmin opreq2
      c2 cexp crrn opreq2d eleq2d anbi12d sumeq1d fveq2d eqeq2d oprabbidv oprex
      cn df-rrn eqid oprabex2 fvopab4 ) FGDAHZIJFHZKLZMLZNZBHZVBNZOZEHZVACHZUSP
      VHVDPUALUCUDLZCQZRPZSZOZABETUSIJDKLZMLZNZVDVONZOZVGVNVICQZRPZSZOZABETZUNU
      EUTDSZVMWBABEWDVFVRVLWAWDVCVPVEVQWDVBVOUSWDVAVNIMUTDJKUBZUFZUGWDVBVOVDWFU
      GUHWDVKVTVGWDVJVSRWDVAVNVICWEUIUJUKUHULABCFGEUOABEVOVOVTWCIVNMUMZWGWCUPUQ
      UR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.  $d X x y d k $.  $d Y x y d k $.
    $( The value of the Euclidean metric.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnmval $p |- ( ( N e. NN /\ X e. ( RR ^m ( 1 ... N ) )
                  /\ Y e. ( RR ^m ( 1 ... N ) ) ) -> ( X ( Rn ` N ) Y ) =
        ( sqr ` sum_ k e. ( 1 ... N ) ( ( ( X ` k ) - ( Y ` k ) ) ^ 2 ) ) ) $=
      ( vx vy vd wcel co cfv cv cmin c2 cexp csu csqr wceq wa fveq1 opreq1d cfz
      cn cmap crrn copab2 rrnval opreqd fvex sumeq2sdv fveq2d opreq2d oprabval2
      cr c1 eqid sylan9eq 3impb ) BUBHZCUMUNBUAIZUCIZHZDUTHZCDBUDJZIZUSAKZCJZVE
      DJZLIZMNIZAOZPJZQURVAVBRVDCDEKZUTHFKZUTHRGKUSVEVLJZVEVMJZLIZMNIZAOZPJZQRE
      FGUEZIVKURVCVTCDEFABGUFUGEFGCDUTUTVSVKVTUSVFVOLIZMNIZAOZPJVJPUHVLCQZVRWCP
      WDUSVQWBAWDVPWAMNWDVNVFVOLVEVLCSTTUIUJVMDQZWCVJPWEUSWBVIAWEWAVHMNWEVOVGVF
      LVEVMDSUKTUIUJVTUOULUPUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.
    $( The underlying set of Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrndm $p |- ( N e. NN -> dom dom ( Rn ` N ) = ( RR ^m ( 1 ... N ) ) ) $=
      ( vx vy vd vk wcel cfv cdm cr c1 cfz co cv wa csqr wceq dmeqd c0 wne wn
      cn crrn cmap cxp cmin c2 cexp csu copab2 rrnval fvex eqid dmoprab2 syl6eq
      cc0 0re n0i ax-mp reex oprex map0 simplbi mto df-ne mpbir a1i dmxp eqtrd
      syl ) AUAFZAUBGZHZHIJAKLZUCLZVNUDZHZVNVJVLVOVJVLBMZVNFCMZVNFNDMVMEMZVQGVS
      VRGUELUFUGLEUHZOGZPNBCDUIZHVOVJVKWBBCEADUJQBCDVNVNWAWBVTOUKWBULUMUNQVJVNR
      SZVPVNPWCVJWCVNRPZTWDIRPZUOIFWETUPIUOUQURWDWEVMRSIVMUSJAKUTVAVBVCVNRVDVEV
      FVNVNVGVIVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k z m n $.
    $( Euclidean space is a metric space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnmet $p |- ( N e. NN -> ( Rn ` N ) e. Met ) $=
      ( vx vy vk vm wcel cfv cr wf co cc0 wceq caddc wral wa cmin cexp syl11anc
      c2 csqr vz vd vn cn crrn cdm cxp cv wb cle wbr cme c1 cfz cmap csu copab2
      w3a elnnuz biimpi 3ad2ant1 oprex elmap ffvelrn sylanb 3ad2antl2 3ad2antl3
      cuz reex resubcl resqcl syl ralrimiva fsumrecl sqge0 jca fsumcmp0 resqrcl
      3expib eqid foprab2 sylib rrnval feq1d mpbird rrndm xpeq12d feq2d anbi12d
      ralrimivv eleq2d rrnmval eqeq1d sqr00 fsum00 recn sqeq0 3syl recnd subeq0
      bitrd ralbidva wfn ffn sylbi eqidd biantrurd eqfnfv bitr4d syl2an 3adant1
      cc 3bitrd wi biimpd 3anbi123i copab simpl 3ad2antl1 trirn syl111anc fveq2
      fopab2 adantl opreq12d fvopab4 npncan adantll eqtr2d sumeq2dv fveq2d cneg
      opreq1d negsubdi2 subcl eqcomd 3brtr4d 3expb ancom2s cvv 3eqtr3d 3adantr2
      sqneg 3adant3r3 3adantr1 3exp2 3imp syld ralrimiv sylbid fvex ismet ax-mp
      sylan2b sylanbrc ) AUDFZAUEGZUFUFZUURUGZHUUQIZBUHZCUHZUUQJZKLZUVAUVBLZUIZ
      UVCUAUHZUVAUUQJZUVGUVBUUQJZMJZUJUKZUAUURNZOZCUURNBUURNZUUQULFZUUPUUTHUMAU
      NJZUOJZUVQUGZHUUQIZUUPUVSUVRHUVAUVQFZUVBUVQFZOZUBUHUVPDUHZUVAGZUWCUVBGZPJ
      ZSQJZDUPZTGZLOBCUBUQZIZUUPUWIHFZCUVQNBUVQNUWKUUPUWLBCUVQUVQUUPUVTUWAUWLUU
      PUVTUWAURZUWHHFZKUWHUJUKZUWLUWMAUMVHGFZUWGHFZDUVPNUWNUUPUVTUWPUWAUUPUWPAU
      SUTVAZUWMUWQDUVPUWMUWCUVPFZOZUWFHFZUWQUWTUWDHFZUWEHFZUXAUVTUUPUWSUXBUWAUV
      TUVPHUVAIZUWSUXBHUVPUVAVIUMAUNVBZVCZUVPHUWCUVAVDZVEVFZUWAUUPUWSUXCUVTUWAU
      VPHUVBIZUWSUXCHUVPUVBVIUXEVCZUVPHUWCUVBVDZVEVGZUWDUWEVJRZUWFVKVLZVMUWGDUM
      AVNRZUWMUWPUWQKUWGUJUKZOZDUVPNZUWOUWRUWMUXQDUVPUWTUWQUXPUXNUWTUXAUXPUXMUW
      FVOVLVPVMZUWGDUMAVQRZUWHVRRVSWJBCUBUVQUVQUWIHUWJUWJVTWAWBUUPUVRHUUQUWJBCD
      AUBWCWDWEUUPUUSUVRHUUQUUPUURUVQUURUVQAWFZUYAWGWHWEUUPUVMBCUURUURUUPUVAUUR
      FZUVBUURFZOUWBUVMUUPUYBUVTUYCUWAUUPUURUVQUVAUYAWKUUPUURUVQUVBUYAWKWIUUPUV
      TUWAUVMUWMUVFUVLUWMUVDUWIKLZUWHKLZUVEUWMUVCUWIKDAUVAUVBWLZWMUWMUWNUWOUYDU
      YEUIUXOUXTUWHWNRUWMUYEUWGKLZDUVPNZUWDUWELZDUVPNZUVEUWMUWPUXRUYEUYHUIUWRUX
      SUWGDUMAWORUWMUYGUYIDUVPUWTUYGUWFKLZUYIUWTUXAUWFXLFUYGUYKUIUXMUWFWPUWFWQW
      RUWTUWDXLFZUWEXLFZUYKUYIUIUWTUWDUXHWSUWTUWEUXLWSUWDUWEWTRXAXBUVTUWAUYJUVE
      UIZUUPUVTUVAUVPXCZUVBUVPXCZUYNUWAUVTUXDUYOUXFUVPHUVAXDXEUWAUXIUYPUXJUVPHU
      VBXDXEUYOUYPOZUYJUVPUVPLZUYJOUVEUYQUYRUYJUYQUVPXFXGDUVPUVPUVAUVBXHXIXJXKX
      MXMUWMUVKUAUURUWMUVGUURFZUVGUVQFZUVKUUPUVTUYSUYTXNUWAUUPUYSUYTUUPUURUVQUV
      GUYAWKXOVAUUPUVTUWAUYTUVKXNUUPUVTUWAUYTUVKUUPUVTUWAUYTURZOZUWIUVPUWCUVGGZ
      UWDPJZSQJZDUPZTGZUVPVUCUWEPJZSQJZDUPZTGZMJZUVCUVJUJVUAUUPUXDUXIUVPHUVGIZU
      RZUWIVULUJUKUVTUXDUWAUXIUYTVUMUXFUXJHUVPUVGVIUXEVCXPUUPVUNOZUVPUWCEUHZUVP
      FZUCUHZVUPUVAGZVUPUVGGZPJZLOEUCXQZGZUWCVUQVURVUTVUPUVBGZPJZLOEUCXQZGZMJZS
      QJZDUPZTGZUVPVVCSQJZDUPZTGZUVPVVGSQJZDUPZTGZMJZUWIVULUJVUOUUPUVPHVVBIZUVP
      HVVFIZVVKVVRUJUKUUPVUNXRVUNVVSUUPVUNVVAHFZEUVPNVVSVUNVWAEUVPVUNVUQOZVUSHF
      ZVUTHFZVWAUXDUXIVUQVWCVUMUVPHVUPUVAVDXSVUMUXDVUQVWDUXIUVPHVUPUVGVDVGZVUSV
      UTVJRVMEUCUVPHVVAVVBVVBVTZYCWBYDVUNVVTUUPVUNVVEHFZEUVPNVVTVUNVWGEUVPVWBVW
      DVVDHFZVWGVWEUXIUXDVUQVWHVUMUVPHVUPUVBVDVFVUTVVDVJRVMEUCUVPHVVEVVFVVFVTZY
      CWBYDDAVVBVVFXTYAVUOUWHVVJTVUOUVPUWGVVIDVUOUWSOZUWFVVHSQVWJVVHUWDVUCPJZVU
      HMJZUWFUWSVVHVWLLVUOUWSVVCVWKVVGVUHMEUCUWCVVAVWKUVPVVBVUPUWCLZVUSUWDVUTVU
      CPVUPUWCUVAYBVUPUWCUVGYBZYEVWFUWDVUCPVBYFZEUCUWCVVEVUHUVPVVFVWMVUTVUCVVDU
      WEPVWNVUPUWCUVBYBYEVWIVUCUWEPVBYFZYEYDVUNUWSVWLUWFLZUUPVUNUWSOZUYLVUCXLFZ
      UYMVWQVWRUWDUXDUXIUWSUXBVUMUXGXSWSZVWRVUCVUMUXDUWSVUCHFUXIUVPHUWCUVGVDVGW
      SZVWRUWEUXIUXDUWSUXCVUMUXKVFWSUWDVUCUWEYGYAYHYIYMYJYKVUOVUGVVNVUKVVQMVUOV
      UFVVMTVUOUVPVUEVVLDVUNUWSVUEVVLLUUPVWRVUDYLZSQJZVWKSQJVUEVVLVWRVXBVWKSQVW
      RVWSUYLVXBVWKLVXAVWTVUCUWDYNRYMVWRVUDXLFZVXCVUELVWRVWSUYLVXDVXAVWTVUCUWDY
      ORVUDUUCVLVWRVWKVVCSQUWSVWKVVCLVUNUWSVVCVWKVWOYPYDYMUUAYHYJYKVUOVUJVVPTVU
      OUVPVUIVVODVWJVUHVVGSQUWSVUHVVGLVUOUWSVVGVUHVWPYPYDYMYJYKYEYQUUNUUPUVTUWA
      UVCUWILUYTUYFUUDVUBUVHVUGUVIVUKMUUPUVTUYTUVHVUGLZUWAUUPUYTUVTVXEUUPUYTUVT
      VXEDAUVGUVAWLYRYSUUBUUPUWAUYTUVIVUKLZUVTUUPUYTUWAVXFUUPUYTUWAVXFDAUVGUVBW
      LYRYSUUEYEYQUUFUUGUUHUUIVPVSUUJWJUUQYTFUVOUUTUVNOUIAUEUUKBCUAYTUUQUURUURV
      TUULUUMUUO $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M k n $.  $d X k n $.  $d N k n $.  $d A k $.  $d F k n $.  $d G k n $.
    $d R k n $.
    rrndstprj1.1 $e |- M = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    rrndstprj1.2 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    $( The distance between two points in Euclidean space is greater than the
       distance between the projections onto one coordinate.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj1 $p |- ( ( ( N e. NN /\ A e. ( 1 ... N ) ) /\ ( F e. X /\ G e. X
                    ) ) -> ( ( F ` A ) M ( G ` A ) ) <_ ( F ( Rn ` N ) G ) ) $=
      ( vk wcel c1 co wa cfv c2 cexp cle wbr cr syl11anc cfz cmin cabs csu csqr
      cn cv crrn cuz cc0 wral elfzuz2 adantr cmap eleq2i reex oprex elmap bitri
      wf ffvelrn sylanb adantlr adantll resubcl resqcl syl jca ralrimiva simplr
      sqge0 fveq2 opreq12d opreq1d fsumlt1 syl111anc sylan2b ad2ant2lr ad2ant2l
      wceq ancoms absresq fsumrecl fsumcmp0 sqsqr 3brtr4d wb cc recn abscl 3syl
      absge0 resqrcl sqrge0 syl22anc mpbird remetdval ad2antrl ad2antll rrnmval
      le2sq simpll biimpi ) EUFJZAKEUALZJZMZBFJZCFJZMZMZABNZACNZUBLZUCNZXEIUGZB
      NZXPCNZUBLZOPLZIUDZUENZXLXMDLZBCEUHNLZQXKXOYBQRZXOOPLZYBOPLZQRZXKXNOPLZYA
      YFYGQXKEKUINJZXTSJZUJXTQRZMZIXEUKZXFYIYAQRXGYJXJUFAKEULUMZXKYMIXEXKXPXEJZ
      MZYKYLYQXSSJZYKYQXQSJZXRSJZYRXJYPYSXGXHYPYSXIXHXESBUTZYPYSXHBSXEUNLZJZUUA
      FUUBBHUOZSXEBUPKEUAUQZURUSZXESXPBVAVBVCVDXJYPYTXGXIYPYTXHXIXESCUTZYPYTXIC
      UUBJZUUGFUUBCHUOZSXECUPUUEURUSZXESXPCVAVBVDVDXQXRVETZXSVFVGZYQYRYLUUKXSVK
      VGVHVIZXDXFXJVJXTYIIAKEXPAVTZXSXNOPUUNXQXLXRXMUBXPABVLXPACVLVMVNVOVPXKXNS
      JZYFYIVTXKXLSJZXMSJZUUOXFXHUUPXDXIXHXFUUAUUPUUFUUAXFUUPXESABVAWAVQVRZXFXI
      UUQXDXHXIXFUUGUUQUUJUUGXFUUQXESACVAWAVQVSZXLXMVETZXNWBVGXKYASJZUJYAQRZYGY
      AVTXKYJYKIXEUKUVAYOXKYKIXEUULVIXTIKEWCTZXKYJYNUVBYOUUMXTIKEWDTZYAWETWFXKX
      OSJZUJXOQRZYBSJZUJYBQRZYEYHWGXKUUOXNWHJZUVEUUTXNWIZXNWJWKXKUUOUVIUVFUUTUV
      JXNWLWKXKUVAUVBUVGUVCUVDYAWMTXKUVAUVBUVHUVCUVDYAWNTXOYBXAWOWPXKUUPUUQYCXO
      VTUURUUSXLXMDGWQTXKXDUUCUUHYDYBVTXDXFXJXBXHUUCXGXIXHUUCUUDXCWRXIUUHXGXHXI
      UUHUUIXCWSIEBCWTVPWF $.
      $( [2-Sep-2009] $)

    $( Bound on the distance between two points in Euclidean space given bounds
       on the distances in each coordinate.  This theorem and ~ rrndstprj1 can
       be used to show that the supremum norm and Euclidean norm are
       equivalent.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj2 $p |- ( ( ( N e. NN /\ F e. X /\ G e. X ) /\ ( R e. RR+ /\
                    A. n e. ( 1 ... N ) ( ( F ` n ) M ( G ` n ) ) < R ) ) ->
                                ( F ( Rn ` N ) G ) < ( R x. ( sqr ` N ) ) ) $=
      ( vk wcel cr c1 co clt wbr wa c2 wceq syl11anc cn w3a cfz cmap crp cv cfv
      wral crrn csqr cmul biid eleq2i 3anbi123i cmin cexp rrnmval adantr elnnuz
      csu cuz biimpi 3ad2ant1 wf oprex elmap ffvelrn sylanb 3ad2antl2 3ad2antl3
      reex resubcl resqcl adantlr rpre ad2antlr wi fveq2 opreq12d breq1d rcla4v
      syl imdistani ancoms cabs remetdval opreq1d absresq eqtrd ad2ant2r cc0 wb
      cle cme remet remetba metcl syl111anc metge0 rpge0 syl22anc biimpa anasss
      a1i lt2sq eqbrtrrd sylan2 expr 3jca ralrimiva fsumlt caddc rpcn fsumconst
      imp cc sqcl syl2an nncn ax-1cn sylancl 3ad2antl1 adantrr breqtrd fsumrecl
      npcan sqge0 jca fsumcmp0 sqsqr nnre cn0 nnnn0 nn0ge0 resqrcl recnd mulcom
      sqmul 3eqtr3d sqrge0 3brtr4d remulcl mulge0 mpbird eqbrtrd ) FUAKZCGKZDGK
      ZUBUUFCLMFUCNZUDNZKZDUUJKZUBZAUEKZBUFZCUGZUUODUGZENZAOPZBUUIUHZQZCDFUIUGN
      ZAFUJUGZUKNZOPUUFUUFUUGUUKUUHUULUUFULGUUJCIUMGUUJDIUMUNUUMUVAQZUVBUUIJUFZ
      CUGZUVFDUGZUONZRUPNZJUTZUJUGZUVDOUUMUVBUVLSUVAJFCDUQURUVEUVLUVDOPZUVLRUPN
      ZUVDRUPNZOPZUVEUVKFARUPNZUKNZUVNUVOOUVEUVKUUIUVQJUTZUVROUVEFMVAUGKZUVJLKZ
      UVQLKZUVJUVQOPZUBZJUUIUHUVKUVSOPUUMUVTUVAUUFUUKUVTUULUUFUVTFUSVBZVCZURUVE
      UWDJUUIUVEUVFUUIKZQUWAUWBUWCUUMUWGUWAUVAUUMUWGQZUVILKZUWAUWHUVGLKZUVHLKZU
      WIUUKUUFUWGUWJUULUUKUUILCVDUWGUWJLUUICVKMFUCVEZVFUUILUVFCVGVHVIZUULUUFUWG
      UWKUUKUULUUILDVDUWGUWKLUUIDVKUWLVFUUILUVFDVGVHVJZUVGUVHVLTZUVIVMWBZVNUVAU
      WBUUMUWGUUNUWBUUTUUNALKZUWBAVOZAVMWBURVPUVEUWGUWCUUMUUNUUTUWGUWCVQUUMUUNQ
      ZUUTUWGUWCUUTUWGQUWSUWGUVGUVHENZAOPZQZUWCUWGUUTUXBUWGUUTUXAUUSUXABUVFUUIU
      UOUVFSZUURUWTAOUXCUUPUVGUUQUVHEUUOUVFCVRUUOUVFDVRVSVTWAWCWDUWSUXBQUWTRUPN
      ZUVJUVQOUUMUWGUXDUVJSUUNUXAUWHUXDUVIWEUGZRUPNZUVJUWHUWTUXERUPUWHUWJUWKUWT
      UXESUWMUWNUVGUVHEHWFTWGUWHUWIUXFUVJSUWOUVIWHWBWIWJUWSUWGUXAUXDUVQOPZUWSUW
      GQZUXAUXGUXHUWTLKZWKUWTWMPZUWQWKAWMPZUXAUXGWLUXHEWNKZUWJUWKUXIUXLUXHEHWOX
      DZUUMUWGUWJUUNUWMVNZUUMUWGUWKUUNUWNVNZUVGUVHELEHWPZWQWRUXHUXLUWJUWKUXJUXM
      UXNUXOUVGUVHELUXPWSWRUUNUWQUUMUWGUWRVPUUNUXKUUMUWGAWTZVPUWTAXEXAXBXCXFXGX
      HXCXOXIXJUVJUVQJMFXKTUUMUUNUVSUVRSZUUTUUFUUKUUNUXRUULUUFUUNQZUVSFMUONMXLN
      ZUVQUKNZUVRUUFUVTUVQXPKZUVSUYASUUNUWEUUNAXPKZUYBAXMZAXQWBUVQJMFXNXRUXSUXT
      FUVQUKUUFUXTFSZUUNUUFFXPKMXPKUYEFXSXTFMYFYAURWGWIYBYCYDUUMUVNUVKSZUVAUUMU
      VKLKZWKUVKWMPZUYFUUMUVTUWAJUUIUHUYGUWFUUMUWAJUUIUWPXJUVJJMFYETZUUMUVTUWAW
      KUVJWMPZQZJUUIUHUYHUWFUUMUYKJUUIUWHUWAUYJUWPUWHUWIUYJUWOUVIYGWBYHXJUVJJMF
      YITZUVKYJTURUUMUUNUVOUVRSZUUTUUFUUKUUNUYMUULUXSUVCAUKNZRUPNZUVCRUPNZUVQUK
      NZUVOUVRUUFUVCXPKZUYCUYOUYQSUUNUUFUVCUUFFLKZWKFWMPZUVCLKZFYKZUUFFYLKUYTFY
      MFYNWBZFYOTZYPZUYDUVCAYRXRUXSUYNUVDRUPUUFUYRUYCUYNUVDSUUNVUEUYDUVCAYQXRWG
      UXSUYPFUVQUKUUFUYPFSZUUNUUFUYSUYTVUFVUBVUCFYJTURWGYSYBYCUUAUUMUUNUVMUVPWL
      ZUUTUWSUVLLKZWKUVLWMPZUVDLKZWKUVDWMPZVUGUUMVUHUUNUUMUYGUYHVUHUYIUYLUVKYOT
      URUUMVUIUUNUUMUYGUYHVUIUYIUYLUVKYTTURUUFUUKUUNVUJUULUUNUUFVUJUUNUWQVUAVUJ
      UUFUWRVUDAUVCUUBXRWDYBUUFUUKUUNVUKUULUUNUUFVUKUUNUWQUXKQVUAWKUVCWMPZQVUKU
      UFUUNUWQUXKUWRUXQYHUUFVUAVULVUDUUFUYSUYTVULVUBVUCFYTTYHAUVCUUCXRWDYBUVLUV
      DXEXAYCUUDUUEVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N f g j k m n r s x y z $.
    $( Euclidean space is complete.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrncms $p |- ( N e. NN -> ( Rn ` N ) e. CMet ) $=
      ( vn vj vk vr cn wcel cfv cv wbr wrex wi wral wa co wceq clt crp adantlr
      cr vf vx vm vz vy vg vs crrn cme cdm wf clm cca cms rrnmet wex copab cabs
      cfz cmin ccom cxp cres cle ffvelrn adantrr adantrl jca adantll cmap rrndm
      adantr eleq2d anbi12d biimpd imdistani eqid rrndstprj1 syl adantlrl oprex
      c1 reex elmap syl6bb expcom sylan9 imp remet remetba metcl syl11anc 3expb
      mp3an1 sylan rpre lelttr syl111anc mpand syldan anassrs fveq2 fveq1d fvex
      ad2antlr fvopab4 adantl opreq12d breq1d sylibrd imim2d ralimdva wb simprl
      iscau5 syl6rbb biimpar an32s sylan2 ralrimiva anasss ancom2s fopab2 sylib
      reximdva sylancr 3imtr4d eqcomi breq1 rexeqbidv ad2antrr adantllr lmbrnns
      a1i breq2 wne 3syl ex cc cc0 recms iscms mpbi simpri vtoclri ac6s biimprd
      csqr biimpa eqcomd rpsqrcl rpdivcl ancoms imbi2d rexralbidv rcla4v sylbid
      cdiv nnrp cfn c0 wss fzfi cuz elnnuz biimpi eluzfz1 ne0i ad3antrrr nnssre
      filbcmb w3a cmul 3ad2ant1 rrndstprj2 exp32 syld imp32 rpcn nnre cn0 nnnn0
      nn0ge0 resqrcl recn rpne0 divcan1 3ad2antl1 breqtrd expdimp reximdv 3syld
      an4s ralrimdva eqidd syl3an1 imdistand syland eximdv df-rex sylibr iscms2
      3expa mpd sylanbrc ) AFGZAUHHZUIGZFUXGUJUJZUAIZUKZUXJUBIZUXGULHJZUBUXIKZL
      ZUAUXGUMHZMUXGUNGAUOZUXFUXOUAUXPUXFUXJUXPGZNZUXKUXNUXSUXKNZUXLUXIGZUXMNZU
      BUPZUXNUXTWBAUSOZTUXLUKZUCIZFGZUDIBIZUYFUXJHZHZPNUCUDUQZUYHUXLHZURUTVATTV
      BVCZULHZJZBUYDMZNZUBUPZUYCUXTUYKUEIZUYNJZUETKZBUYDMUYRUXTVUABUYDUXTUYHUYD
      GZNUYKUYMUMHZGZVUAUXSUXKVUBVUDUXFUXKVUBNZUXRVUDUXFVUENZUXRVUDVUFCIZDIZVDJ
      ZVUGUXJHZVUHUXJHZUXGOZEIZQJZLZDFMZCFKZERMZVUIVUGUYKHZVUHUYKHZUYMOZVUMQJZL
      ZDFMZCFKZERMZUXRVUDVUFVUQVVEERVUFVUMRGZNZVUPVVDCFVVHVUGFGZNZVUOVVCDFVVJVU
      HFGZNZVUNVVBVUIVVLVUNUYHVUJHZUYHVUKHZUYMOZVUMQJZVVBVVHVVIVVKVUNVVPLZVVHVV
      IVVKNZVUJUXIGZVUKUXIGZNZVVQVUFVVRVWAVVGVUEVVRVWAUXFUXKVVRVWAVUBUXKVVRNVVS
      VVTUXKVVIVVSVVKFUXIVUGUXJVEVFUXKVVKVVTVVIFUXIVUHUXJVEZVGVHSVISVVHVWANZVVO
      VULVDJZVUNVVPVUFVWAVWDVVGUXFVUBVWAVWDUXKUXFVUBNZVWANZVWEVUJTUYDVJOZGZVUKV
      WGGZNZNVWDVWEVWAVWJVWEVWAVWJVWEVVSVWHVVTVWIVWEUXIVWGVUJUXFUXIVWGPZVUBAVKZ
      VLZVMVWEUXIVWGVUKVWMVMVNVOVPUYHVUJVUKUYMAVWGUYMVQZVWGVQZVRVSVTSVWCVVOTGZV
      ULTGZVUMTGZVWDVUNNVVPLVUFVWAVWPVVGUXFVUBVWAVWPUXKVWFVVMTGZVVNTGZVWPVWEVVS
      VWSVVTVWEVVSVWSUXFVVSUYDTVUJUKZVUBVWSUXFVVSVXAUXFVVSVWHVXAUXFUXIVWGVUJVWL
      VMTUYDVUJWCWBAUSWAZWDWEVOVXAVUBVWSUYDTUYHVUJVEWFWGWHVFVWEVVTVWTVVSVWEVVTV
      WTUXFVVTUYDTVUKUKZVUBVWTUXFVVTVXCUXFVVTVWIVXCUXFUXIVWGVUKVWLVMZTUYDVUKWCV
      XBWDWEVOVXCVUBVWTUYDTUYHVUKVEWFWGWHVGUYMUIGZVWSVWTVWPUYMVWNWIZVVMVVNUYMTU
      YMVWNWJZWKWNWLVTSVUFVWAVWQVVGUXFVWAVWQVUEUXFUXHVWAVWQUXQUXHVVSVVTVWQVUJVU
      KUXGUXIUXIVQZWKWMWOSSVVGVWRVUFVWAVUMWPXEVVOVULVUMWQWRWSWTXAVVLVVAVVOVUMQV
      VLVUSVVMVUTVVNUYMVVIVUSVVMPVVHVVKUCUDVUGUYJVVMFUYKUYFVUGPUYHUYIVUJUYFVUGU
      XJXBXCUYKVQZUYHVUJXDXFXEVVKVUTVVNPVVJUCUDVUHUYJVVNFUYKUYFVUHPUYHUYIVUKUYF
      VUHUXJXBXCVXIUYHVUKXDXFZXGXHXIXJXKXLYEXLVUFUXHUXKUXRVURXMUXFUXHVUEUXQVLUX
      FUXKVUBXNEUXGCDUXJUXIVXHXOWLVUFVXEFTUYKUKZVUDVVFXMVXFVUFUYJTGZUCFMZVXKUXF
      VUBUXKVXMUXFVUBUXKVXMVWEUXKNVXLUCFVWEUXKUYGVXLUXKUYGNZVWEUYIUXIGZVXLFUXIU
      YFUXJVEZUXFVXOVUBVXLUXFVXONUYDTUYIUKZVUBVXLUXFVXQVXOUXFVXOUYIVWGGVXQUXFUX
      IVWGUYIVWLVMTUYDUYIWCVXBWDXPXQZUYDTUYHUYIVEZWOXRXSXAXTYAYBUCUDFTUYJUYKVXI
      YCZYDEUYMCDUYKTVXGXOYFYGWHXRXAUFIZUYSUYNJZUEUYMUJUJZKZVUAUFUYKVUCVYAUYKPZ
      VYBUYTUEVYCTVYCTPVYETVYCVXGYHYNVYAUYKUYSUYNYIYJVXEVYDUFVUCMZUYMUNGVXEVYFN
      UUAUEUYMUFVYCVYCVQUUBUUCUUDUUEVSXTUYTUYOBUEUYDTUBVXBUYSUYLUYKUYNYOUUFVSUX
      TUYQUYBUBUXTUYEUYAUYPUYBUXTUYAUYEUXTUYAUXLVWGGZUYEUXTUXIVWGUXLUXFVWKUXRUX
      KVWLYKVMTUYDUXLWCVXBWDZWEUUGUXTUYAUYPUXMUXFUXKUYAUYPUXMLZLUXRUXFUXKNZUYAV
      YIVYJUYANZUYPVUIVUKUXLUXGOZVUMQJZLZDFMZCFKZERMZUXMVYKUYPVYPERVYKVVGNZUYPV
      UIVVNUYLUYMOZVUMAUUHHZUUROZQJZLZDFMCFKZBUYDMZVUIWUBBUYDMZLZDFMZCFKZVYPVYR
      UYOWUDBUYDVYRVUBNZUYOVUIVYSUGIZQJZLZDFMCFKZUGRMZWUDWUJVXEUYLTGZVXKUYOWUOX
      MVXEWUJVXFYNVYKVUBWUPVVGUXFUYAVUBWUPUXKUXFUYANZUYEVUBWUPWUQVYGUYEUXFUYAVY
      GUXFUXIVWGUXLVWLVMZUUIVYHYDUYDTUYHUXLVEWOYLSWUJVXMVXKVYKVUBVXMVVGVYJVUBVX
      MUYAVYJVUBNVXLUCFVYJUYGVUBVXLVYJUYGNVXQVUBVXLUXFUXKUYGVXQVXNUXFVXOVXQVXPV
      XRXSXAVXSWOXRXTSSVXTYDUGVVNUYMUYLCDUYKTVXGVVKVUTVVNVXJUUJYMWRVYRWUOWUDLZV
      UBVYJVVGWUSUYAUXFVVGWUSUXKUXFVVGNZWUARGZWUSVVGUXFWVAUXFVVGVYTRGZWVAUXFARG
      ZWVBAUUSZAUUKZVSVUMVYTUULXSZUUMWUNWUDUGWUARWUKWUAPZWUMWUCCDFFWVGWULWUBVUI
      WUKWUAVYSQYOUUNUUOUUPVSSSVLUUQXLVYRUYDUUTGZUYDUVAYPZFTUVBZWUEWUILWVHVYRWB
      AUVCYNUXFWVIUXKUYAVVGUXFAWBUVDHGZWBUYDGWVIUXFWVKAUVEUVFWBAUVGUYDWBUVHYQUV
      IWVJVYRUVJYNWUBBCDUYDFUVKWRVYRWUHVYOCFVYRWUGVYNDFVYRVVKNZWUFVYMVUIWVLVYRV
      VTNWUFVYMLZVYRVVKVVTVYJVVKVVTLZUYAVVGUXKWVNUXFUXKVVKVVTVWBYRXGYKVPVYKVVGV
      VTWVMUXFUYAVVGVVTNWVMUXKUXFVVGUYAVVTWVMUXFUYAVVTNZVVGWVMUXFWVONUXFVWIVYGN
      ZNZVVGWVMUXFVVTUYAWVQUXFVVTUYANZWVPUXFWVRWVPUXFVVTVWIUYAVYGVXDWURVNVOVPYB
      WVQVVGWUFVYMUXFVWIVYGVVGWUFNZVYMLUXFVWIVYGUVLZWVSVYMWVTWVSNVYLWUAVYTUVMOZ
      VUMQWVTVVGWUFVYLWWAQJZWVTVVGWVAWUFWWBLUXFVWIVVGWVALVYGVVGUXFWVAWVFWFUVNWV
      TWVAWUFWWBWUABVUKUXLUYMAVWGVWNVWOUVOUVPUVQUVRWVTVVGWWAVUMPZWUFUXFVWIVVGWW
      CVYGWUTVUMYSGZVYTYSGZVYTYTYPZWWCVVGWWDUXFVUMUVSXGUXFWWEVVGUXFATGZYTAVDJZN
      VYTTGWWEUXFWWGWWHAUVTUXFAUWAGWWHAUWBAUWCVSVHAUWDVYTUWEYQVLUXFWWFVVGUXFWVC
      WVBWWFWVDWVEVYTUWFYQVLVUMVYTUWGWRUWHVFUWIYRWMUWJWOXRUWMYLXAVSXKXLUWKUWLUW
      NUXFUYAUXKUXMVYQXMZUXFUYAUXKWWIUXFUXHUYAUXKWWIUXQEVUKUXGUXLCDUXJUXIVXHVVK
      VUKUWOYMUWPUXCXRXJYRSUWQUWRUWSUXDUXMUBUXIUWTUXAYRXTUBUXGUAUXIVXHUXBUXE $.
      $( [28-Feb-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d X a b d f m n r u v w x y z $.  $d M a b d f m n r u v w x y z $.
    $d Y a b d f m n r u v w x y z $.  $d N a b d f m n r u v w x y z $.
    rrntotbnd.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrntotbnd.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem1 $p |- ( ( ( ( N e. NN /\ x e. X ) /\ ( d e. RR+ /\ r e. RR+ )
          ) /\ ( y e. dom dom ( Rn ` N ) /\ ( x ( Rn ` N ) y ) < r ) ) ->
        ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
            ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\
            v = ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( ball ` ( Rn ` N ) ) d ) e.
        { a | E. f ( f : ( 1 ... N ) --> ( -u ( ( |_ ` ( r / ( d / ( sqr ` N )
            ) ) ) + 1 ) ... ( ( |_ ` ( r / ( d / ( sqr ` N ) ) ) ) + 1 ) ) /\
                a = ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
                                ( ( d / ( sqr ` N ) ) x. ( f ` m ) ) ) ) }
                                            ( ball ` ( Rn ` N ) ) d ) ) } ) $=
      ( wcel wa co cr cn cv crp crrn cfv cdm clt wbr c1 cfz csqr cdiv cfl caddc
      cneg wf cmin c2 wceq copab cmul cbl wex cab wral cabs cle cn0 simplr nnrp
      cz rpsqrcl syl rpdivcl sylan2 ancoms adantlr syl11anc rpre rpge0 flge0nn0
      cc0 nn0p1nn 3syl an31s ad2antrr cmap rrndm eleq2d reex oprex elmap syl6bb
      biimpa ffvelrn sylan adantllr eleq2i bitri adantll resubcl ad2ant2r rpne0
      wne redivcl syl111anc 2re 2ne0 rereccli readdcl sylancl flcl adantlrr zcn
      sylanb cc abscl recnd peano2zdi zre anasss peano2re absrdbnd absdiv absid
      opreq2d eqtrd cme adantr a1i wi eqbrtrd letrd sylib mpbid 3ad2ant1 eqeq2d
      eqid wb anbi2d recn syl6reqr simpr metcl ad2antll ccom cxp cres remetdval
      rrnmet remet remetba metsym biimprd imdistani rrndstprj1 an32s ex anassrs
      imp eqbrtrrd adantl ad2antlr ltle impr elrp lediv1 flwordi 1re leadd1 w3a
      nnre absle syl2an biimp3a simp2 nnnegz elfz mpbird ralrimiva fopab2 eqidd
      nnz opabex2 feq1 fveq1 opabbidv opreq1d anbi12d cla4ev exbidv elab sylibr
      eqeq1 ) IUAQZAUBZJQZRZNUBZUCQZLUBZUCQZRZRZBUBZIUDUEZUFUFZQZUWPUXEUXFSZUXA
      UGUHZRZRZUIIUJSZUXAUWSIUKUEZULSZULSZUMUEZUIUNSZUOZUXRUJSZEUBZUPZFUBZUXMQZ
      DUBZUYCUWPUEZUXOUYCGUBZUXMQZCUBUYGUXEUEZUYGUWPUEZUQSZUXOULSZUIURULSZUNSZU
      MUEZUSRGCUTZUEZVASZUNSZUSZRZFDUTZUWSUXFVBUEZSZUYDUYEUYFUXOUYCUYAUEZVASZUN
      SZUSZRZFDUTZUWSVUCSZUSZRZEVCZVUDUYBMUBZVUKUSZRZEVCZMVDQUXLUXMUXTUYPUPZVUD
      VUDUSZVUNUXLUYOUXTQZGUXMVEVUSUXLVVAGUXMUXLUYHRZUXRUAQZUYOVKQZUYOVFUEZUXRV
      GUHZVVAUXDVVCUXKUYHUWOUXCVVCUWQUXCUWOVVCUWOUXBUWTVVCUWOUXBRUWTRZUXPUCQZUX
      QVHQZVVCVVGUXBUXOUCQZVVHUWOUXBUWTVIUWOUWTVVJUXBUWTUWOVVJUWOUWTUXNUCQZVVJU
      WOIUCQVVKIVJIVLVMUWSUXNVNVOZVPZVQUXAUXOVNZVRVVHUXPTQZWBUXPVGUHVVIUXPVSZUX
      PVTUXPWAVRUXQWCWDWEVPVQWFUXDUXHUYHVVDUXJUXDUXHRZUYHRZUYNTQZVVDVVRUYLTQZUY
      MTQVVSVVRUYKTQZUXOTQZUXOWBXDZVVTUWRUXHUYHVWAUXCUWRUXHRZUYHRZUYITQZUYJTQZV
      WAUWOUXHUYHVWFUWQUWOUXHRUXMTUXEUPZUYHVWFUWOUXHVWHUWOUXHUXETUXMWGSZQVWHUWO
      UXGVWIUXEIWHZWITUXMUXEWJUIIUJWKZWLWMWNUXMTUYGUXEWOWPWQZUWRUYHVWGUXHUWQUYH
      VWGUWOUWQUXMTUWPUPZUYHVWGUWQUWPVWIQVWMJVWIUWPOWRTUXMUWPWJVWKWLWSUXMTUYGUW
      PWOXOWTVQZUYIUYJXAVRWQZUXDVWBUXHUYHUWOUWTVWBUWQUXBUWTUWOVWBUWTUWORZVVJVWB
      VVLUXOVSVMVPZXBWFUXDVWCUXHUYHUWOUWTVWCUWQUXBUWTUWOVWCVWPVVJVWCVVLUXOXCVMV
      PXBZWFUYKUXOXEXFZURXGXHXIUYLUYMXJXKZUYNXLZVMXMVVBVVEUYLVFUEZUMUEZUIUNSZUX
      RVVBUYOXPQZVVETQVVBVVSVVDVXEUXDUXHUYHVVSUXJVWTXMVXAUYOXNWDUYOXQVMVVBVXBTQ
      ZVXDVKQVXDTQUXDUXHUYHVXFUXJVVRUYLXPQVXFVVRUYLVWSXRUYLXQVMXMZVXFVXCVXBXLZX
      SVXDXTWDVVBUXQTQZUXRTQZUXDVXIUXKUYHUWOUXCVXIUWQUWOUXCRVVOUXQVKQVXIUWOUWTU
      XBVVOUXBUWOUWTRZVVOUXBVXKRVVHVVOVXKUXBVVJVVHVVMVVNVOVVPVMVPYAZUXPXLUXQXTW
      DVQWFZUXQYBVMVVBVVTVVEVXDVGUHUXDUXHUYHVVTUXJVWSXMUYLYCVMVVBVXCUXQVGUHZVXD
      UXRVGUHZVVBVXFVVOVXBUXPVGUHVXNVXGUXDVVOUXKUYHUWOUXCVVOUWQVXLVQWFVVBVXBUYK
      VFUEZUXOULSZUXPVGVVBVXBVXPUXOVFUEZULSZVXQVVBUYKXPQZUXOXPQZVWCVXBVXSUSVVBU
      YKUXDUXHUYHVWAUXJVWOXMZXRUXDVYAUXKUYHUWOUWTVYAUWQUXBVXKUXOVWQXRXBWFUXDVWC
      UXKUYHVWRWFUYKUXOYDXFVVBVXRUXOVXPULUXDVXRUXOUSZUXKUYHUWOUWTVYCUWQUXBVXKVW
      BWBUXOVGUHZVYCVWQVXKVVJVYDVVMUXOVTVMUXOYEVRXBWFYFYGVVBVXPUXAVGUHZVXQUXPVG
      UHZVVBVXPUXIUXAVVBVWAVXTVXPTQZVYBUYKUUAUYKXQWDZUXLUXITQZUYHUWRUXHVYIUXCUX
      JVWDUXFYHQZUWPUXGQZUXHVYIUWOVYJUWQUXHIUUJWFUWRVYKUXHUWOUWQVYKUWOJUXGUWPUW
      OUXGVWIJVWJOUUBZWIWNYIUWRUXHUUCUWPUXEUXFUXGUXGYRUUDXFZXBYIUXDUXATQZUXKUYH
      UXBVYNUWRUWTUXAVSZUUEWFZUWRUXKUYHVXPUXIVGUHZUXCUWRUXHUYHVYQUXJVWEUYIUYJVF
      UQUUFTTUUGUUHZSZVXPUXIVGVWEVWFVWGVYSVXPUSVWLVWNUYIUYJVYRVYRYRZUUIVRVWEVYS
      UYJUYIVYRSZUXIVGVWEVYRYHQZVWFVWGVYSWUAUSWUBVWEVYRVYTUUKYJVWLVWNUYIUYJVYRT
      VYRVYTUULUUMXFVWDUWRUXEJQZRZUYHWUAUXIVGUHZUWRUXHWUCUWOUXHWUCYKUWQUWOWUCUX
      HUWOJUXGUXEVYLWIUUNYIUUOWUDUYHWUEUWOUWQWUCUYHWUEYKUWOUWQWUCRZRUYHWUEUWOUY
      HWUFWUEUYGUWPUXEVYRIJVYTOUUPUUQUURUUSUUTWPYLUVAXMWQUXLUXIUXAVGUHZUYHUXDUX
      HUXJWUGVVQVYIVYNUXJWUGYKUWRUXHVYIUXCVYMVQUXCVYNUWRUXHUXBVYNUWTVYOUVBUVCUX
      IUXAUVDVRUVEYIYMVVBVYGVYNVWBWBUXOUGUHRZVYEVYFYSVYHVYPVVBVVJWUHUXDVVJUXKUY
      HUWOUWTVVJUWQUXBVVMXBWFUXOUVFYNVXPUXAUXOUVGXFYOYLVXBUXPUVHXFVVBVXCTQZVXIU
      ITQZVXNVXOYSVVBVXFVXCVKQWUIVXGVXHVXCXTWDVXMWUJVVBUVIYJVXCUXQUIUVJXFYOYMVV
      CVVDVVFUVKZVVAUXSUYOVGUHUYOUXRVGUHRZVVCVVDVVFWULVVDVVCVVFWULYSZVVDUYOTQVX
      JWUMVVCUYOXTUXRUVLUYOUXRUVMUVNVPUVOWUKVVDUXSVKQZUXRVKQZVVAWULYSVVCVVDVVFU
      VPVVCVVDWUNVVFUXRUVQYPVVCVVDWUOVVFUXRUWCYPUYOUXSUXRUVRXFUVSXFUVTGCUXMUXTU
      YOUYPUYPYRUWAYNUXLVUDUWBVUMVUSVUTREUYPGCUXMUYOVWKUWDUYAUYPUSZUYBVUSVULVUT
      UXMUXTUYAUYPUWEWUPVUKVUDVUDWUPVUJVUBUWSVUCWUPVUIVUAFDWUPVUHUYTUYDWUPVUGUY
      SUYEWUPVUFUYRUYFUNWUPVUEUYQUXOVAUYCUYAUYPUWFYFYFYQYTUWGUWHYQUWIUWJVRVURVU
      NMVUDVUBUWSVUCWKVUOVUDUSZVUQVUMEWUQVUPVULUYBVUOVUDVUKUWNYTUWKUWLUWM $.
      $( [2-Sep-2009] $)

    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem2 $p |- ( ( ( N e. NN /\ d e. RR+ ) /\
          ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) ) ->
                  ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
        ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\ v =
                ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                      ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( Rn ` N ) y ) < d ) $=
      ( wcel wa cr co cfv wceq cmul va cn cv crp c1 cfz cmap csqr cdiv c2 caddc
      cmin cfl copab crrn clt cabs ccom cxp cres wbr wral simpll wf oprex elmap
      reex ffvelrn sylanb adantll adantlrr nnrp rpsqrcl syl rpdivcl sylan2 rpre
      ancoms ad2antrr cz cc0 wne adantlr resubcl syl11anc redivcl syl111anc 2re
      rpne0 2ne0 rereccli readdcl sylancl flcl 3syl ralrimiva eqid fopab2 sylib
      sylan remulcl sylibr simprr adantr fveq2 opreq2d opreq12d fvopab4 opreq1d
      zre adantl fveq2d fvex jca simprl remetdval recnd subcl abscl a1i 1re cle
      cc rddif halflt1 lelttrd wb rpgt0 ltmul2 syl112anc recn ad2antrl ad2antll
      mpbid addcom subsub3 rpcn divcan2 eqcomd eqbrtrd subdi eqtr4d rpge0 absid
      3eqtr2d absmul 3eqtrd 3brtr4d anassrs rrndstprj2 syl32anc divcan1 breqtrd
      mulid1 syl2an ) HUBNZKUCZUDNZOZAUCZPUEHUFQZUGQZNZBUCZUVBNZOZOZEUCZUVANZDU
      CUVHUUTRZUUQHUHRZUIQZUVHFUCZUVANZCUCUVMUVDRZUVMUUTRZULQZUVLUIQZUEUJUIQZUK
      QZUMRZSOFCUNZRZTQZUKQZSOEDUNZUVDHUORQZUVLUVKTQZUUQUPUVGUUPUWFUVBNZUVEUVLU
      DNZUAUCZUWFRZUWKUVDRZUQULURPPUSUTZQZUVLUPVAZUAUVAVBUWGUWHUPVAUUPUURUVFVCU
      VGUVAPUWFVDZUWIUVGUWEPNZEUVAVBUWQUVGUWREUVAUVGUVIOZUVJPNZUWDPNZUWRUUSUVCU
      VIUWTUVEUVCUVIUWTUUSUVCUVAPUUTVDZUVIUWTPUVAUUTVGUEHUFVEZVFZUVAPUVHUUTVHVI
      VJVKUWSUVLPNZUWCPNZUXAUUSUXEUVFUVIUURUUPUXEUURUUPOZUWJUXEUUPUURUVKUDNZUWJ
      UUPHUDNZUXHHVLZHVMZVNUUQUVKVOVPZUVLVQZVNVRZVSUVGUVAPUWBVDZUVIUXFUVGUWAPNZ
      FUVAVBUXOUVGUXPFUVAUVGUVNOZUVTPNZUWAVTNUXPUXQUVRPNZUVSPNZUXRUXQUVQPNZUXEU
      VLWAWBZUXSUVFUVNUYAUUSUVFUVNOUVOPNZUVPPNZUYAUVEUVNUYCUVCUVEUVAPUVDVDZUVNU
      YCPUVAUVDVGUXCVFZUVAPUVMUVDVHVIVJUVCUVNUYDUVEUVCUXBUVNUYDUXDUVAPUVMUUTVHV
      IWCUVOUVPWDWEVJUUSUXEUVFUVNUXNVSUUSUYBUVFUVNUURUUPUYBUXGUWJUYBUXLUVLWIZVN
      VRVSUVQUVLWFWGUJWHWJWKZUVRUVSWLWMUVTWNUWAXJWOWPFCUVAPUWAUWBUWBWQZWRWSUVAP
      UVHUWBVHWTUVLUWCXAWEUVJUWDWLWEWPEDUVAPUWEUWFUWFWQZWRWSPUVAUWFVGUXCVFXBUUS
      UVCUVEXCUUSUWJUVFUURUUPUWJUXLVRZXDUVGUWPUAUVAUVGUWKUVANZOZUWOUWKUUTRZUVLU
      WKUWBRZTQZUKQZUWMUWNQZUVLUPUYLUWOUYRSUVGUYLUWLUYQUWMUWNEDUWKUWEUYQUVAUWFU
      VHUWKSZUVJUYNUWDUYPUKUVHUWKUUTXEUYSUWCUYOUVLTUVHUWKUWBXEXFXGUYJUYNUYPUKVE
      XHXIXKUYMUYRUYNUVLUWMUYNULQZUVLUIQZUVSUKQZUMRZTQZUKQZUWMUWNQZUVLUPUYLUYRV
      UFSUVGUYLUYQVUEUWMUWNUYLUYPVUDUYNUKUYLUYOVUCUVLTFCUWKUWAVUCUVAUWBUVMUWKSZ
      UVTVUBUMVUGUVRVUAUVSUKVUGUVQUYTUVLUIVUGUVOUWMUVPUYNULUVMUWKUVDXEUVMUWKUUT
      XEXGXIXIXLUYIVUBUMXMXHXFXFXIXKUUSUVFUYLVUFUVLUPVAZUUSUWJUYNPNZUWMPNZOZVUH
      UVFUYLOZUYKVULVUIVUJUVCUYLVUIUVEUVCUXBUYLVUIUXDUVAPUWKUUTVHVIWCUVEUYLVUJU
      VCUVEUYEUYLVUJUYFUVAPUWKUVDVHVIVJXNUWJVUKOZVUFVUEUWMULQZUQRZUVLUPVUMVUEPN
      ZVUJVUFVUOSVUMVUIVUDPNZVUPUWJVUIVUJXOVUMUXEVUCPNZVUQUWJUXEVUKUXMXDZVUMVUB
      PNZVUCVTNVURVUMVUAPNZUXTVUTVUMUYTPNZUXEUYBVVAVUKVVBUWJVUJVUIVVBUWMUYNWDVR
      XKZVUSUWJUYBVUKUYGXDZUYTUVLWFWGZUYHVUAUVSWLWMVUBWNVUCXJWOZUVLVUCXAWEZUYNV
      UDWLWEUWJVUIVUJXCVUEUWMUWNUWNWQZXPWEVUMUVLVUCVUAULQZUQRZTQZUVLUETQZVUOUVL
      UPVUMVVJUEUPVAZVVKVVLUPVAZVUMVVJUVSUEVUMVVIYCNZVVJPNZVUMVUCYCNZVUAYCNZVVO
      VUMVUCVVFXQZVUMVUAVVEXQZVUCVUAXRWEZVVIXSVNZUXTVUMUYHXTUEPNZVUMYAXTZVUMVVA
      VVJUVSYBVAVVEVUAYDVNUVSUEUPVAVUMYEXTYFVUMVVPVWCUXEWAUVLUPVAZVVMVVNYGVWBVW
      DVUSUWJVWEVUKUVLYHXDVVJUEUVLYIYJYNVUMVUOUVLVVITQZUQRZUVLUQRZVVJTQZVVKVUMV
      UNVWFUQVUMVUNVUDUYNUKQZUWMULQZVUDUYTULQZVWFVUMVUEVWJUWMULVUMUYNYCNZVUDYCN
      ZVUEVWJSVUIVWMUWJVUJUYNYKYLZVUMVUDVVGXQZUYNVUDYOWEXIVUMVWNUWMYCNZVWMVWLVW
      KSVWPVUJVWQUWJVUIUWMYKYMVWOVUDUWMUYNYPWGVUMVWLVUDUVLVUATQZULQZVWFVUMUYTVW
      RVUDULVUMVWRUYTVUMUYTYCNUVLYCNZUYBVWRUYTSVUMUYTVVCXQUWJVWTVUKUVLYQZXDZVVD
      UYTUVLYRWGYSXFVUMVWTVVQVVRVWFVWSSVXBVVSVVTUVLVUCVUAUUAWGUUBUUEXLVUMVWTVVO
      VWGVWISVXBVWAUVLVVIUUFWEVUMVWHUVLVVJTVUMUXEWAUVLYBVAZVWHUVLSVUSUWJVXCVUKU
      VLUUCXDUVLUUDWEXIUUGVUMVVLUVLUWJVVLUVLSZVUKUWJVWTVXDVXAUVLUUNVNXDYSUUHYTU
      UOUUIYTYTWPUVLUAUWFUVDUWNHUVBVVHUVBWQUUJUUKUUSUWHUUQSZUVFUUSUUQYCNZUVKYCN
      ZUVKWAWBZVXEUURVXFUUPUUQYQXKUUPVXGUURUUPUXIUXHVXGUXJUXKUVKYQWOXDUUPVXHUUR
      UUPUXIUXHVXHUXJUXKUVKWIWOXDUUQUVKUULWGXDUUM $.
      $( [2-Sep-2009] $)

    $( A set in Euclidean space is totally bounded iff its is bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrntotbnd $p |- ( ( N e. NN /\ Y C_ X ) ->
                                      ( M e. TotBnd <-> M e. Bnd ) ) $=
      ( vx vb vm wcel wss wa cv cfv co wceq crp cfn cr c0 vr vv vy ctotbnd cbnd
      vd vf va vu vz vn cdm eqid totbndbnd cbl wrex wral cuni crrn cxp cres a1i
      cn dmeqd cme rrnmet adantr cfz cmap rrndm syl6eqr sseq2d biimpar metssba2
      c1 syl11anc eqtr4d raleqdv wi 0fin id 0ss eqsstrd adantl ral0 unieq raleq
      anbi12d rcla4ev ralrimiva wne cdiv cfl caddc wf cmul copab wex cab anim2i
      anassrs clt wbr wb ad2antrr eleq2d simprr syl111anc c2 biimpi oprex elmap
      reex ffvelrn sylanb adantll adantlrr syl sylan2 ancoms cz adantlr readdcl
      3syl fopab2 sylib sylan remulcl syl6bb mpbird ad2ant2rl adantrr ex sylbid
      an32s sstri fzfi mp2an cvv ax-mp jctir sylancr a1d df-ne r19.2z csqr cneg
      wn anim1i anasss syl6reqr biimpa elbl cmin rrntotbndlem1 biimpd imdistani
      ssel2 eleq2i rrntotbndlem2 simplr nnrp rpsqrcl rpdivcl rpre resubcl rpne0
      cc0 redivcl 2re 2ne0 rereccli sylancl flcl zre elbl2 syl22anc imp sylanl1
      expr eleq2 eluni2 sylibr an4s ssrdv eqeq1d jca cin blssp inss1 syl5ibrcom
      sseq1 sylbird sstr expcom sylsyld visset eqeq1 anbi2d exbidv bitri fzssuz
      elab cuz uzssz zssre fss mpan2 syl2an opreq1 eqeq2d exlimdv syl5bi jctird
      ralrimiv xpfi pwfi mpbi mapsspw ssfi eqeltrri firnfi4 mpan syl6 ralrimdva
      mapval rexlimdvv syl5 expdimp sylan2br pm2.61dan metres syl5eqel sstotbnd
      cpw isbnd2 3imtr4d impbid2 ) BVCJZDCKZLZAUDJZAUEJZAAULZULZVUEUMZUNVUAVUEG
      MZUAMZAUONOZPZUAQUPZGVUEUQZDUBMZURZKZHMZUCMZUFMZBUSNZUONZOZPZUCVUSULULZUP
      ZHVUMUQZLZUBRUPZUFQUQZVUCVUBVUAVULVUKGDUQZVVHVUAVUKGVUEDVUAVUEVUSDDUTVAZU
      LZULZDVUAVUDVVKVUAAVVJAVVJPVUAFVBVDVDZVUAVUSVEJZDVVCKZDVVLPUYSVVNUYTBVFZV
      GZUYSVVOUYTUYSVVCCDUYSVVCSVOBVHOZVIOZCBVJZEVKVLVMZVUSVVCDVVCUMZVNVPZVQVRV
      UADTPZVVIVVHVSZVUAVWDLZVVHVVIVWFVVGUFQVWFVVGVURQJZVWFTRJDTURZKZVVDHTUQZLZ
      VVGVTVWFVWIVWJVWDVWIVUAVWDDTVWHVWDWATVWHKVWDVWHWBVBWCWDVVDHWEUUAVVFVWKUBT
      RVUMTPZVUOVWIVVEVWJVWLVUNVWHDVUMTWFVLVVDHVUMTWGWHWIUUBVGWJUUCVWDUUHVUADTW
      KZVWEDTUUDVUAVWMVVIVVHVWMVVILVUKGDUPVUAVVHVUKGDUUEVUAVUJVVHGUADQVUAVUGDJZ
      VUHQJZLZVUJVVHVSVUAVWPLZVUJVVGUFQVWQVWGLZVUJDVVRVUHVURBUUFNZWLOZWLOWMNVOW
      NOZUUGZVXAVHOZUGMZWOZUHMZIMZVVRJZUIMZVXGVUGNZVWTVXGVXDNZWPOZWNOZPLIUIWQZV
      URVUTOZPZLZUGWRZUHWSZURZKZVVDHVXSUQZLZVVGVWRVUJVYAVYBVWRVUGVUHVUTOZVXTKZV
      UJDVYDKZVYAVWRUCVYDVXTVWQUYSVUGCJZVWOLZLZVWGVUQVYDJZVUQVXTJZVSZUYSUYTVWPV
      YIUYTVWPLVYHUYSUYTVWNVWOVYHUYTVWNLZVYGVWODCVUGUURZUUIUUJWTXAUYSVWGVYHVYLU
      YSVYGVWGVWOVYLUYSVYGLZVWGVWOLZLZVYJVUQVVCJZVUGVUQVUSOVUHXBXCZLZVYKVYQVVNV
      UGVVCJZVWOVYJVYTXDUYSVVNVYGVYPVVPXEVYOWUAVYPUYSVYGWUAUYSCVVCVUGUYSVVCVVSC
      VVTEUUKXFUULVGVYOVWGVWOXGVUQVUSVUGVUHVVCVWBUUMXHVYQVYTVYKVYQVYTLZVUQUJMZJ
      ZUJVXSUPZVYKWUBVXHVXIVXJVWTVXGUKMZVVRJZVUMWUFVUQNZWUFVUGNZUUNOZVWTWLOZVOX
      IWLOZWNOZWMNZPLUKUBWQZNZWPOZWNOZPLIUIWQZVURVUTOZVXSJVUQWUTJZWUEGUCUBUIUGI
      UKABCDUAUHUFEFUUOVYQVYRWVAVYSVYOVWGVYRWVAVWOVYOVWGLZVYRLWVBVUQVVSJZLWVAWV
      BVYRWVCUYSVYRWVCVSVYGVWGUYSVYRWVCUYSVVCVVSVUQVVTXFZUUPXEUUQVYOUYSVUGVVSJZ
      LZVWGWVCWVAVYGWVEUYSVYGWVECVVSVUGEUUSZXJWTWVFVWGLWVCWVAUYSVWGWVEWVCWVAVSU
      YSVWGLZWVEWVCWVAWVHWVEWVCLZLZWVAWUSVUQVUSOVURXBXCZGUCUBUIIUKABCDUFEFUUTWV
      JVVNVWGWUSVVCJZVYRWVAWVKXDUYSVVNVWGWVIVVPXEUYSVWGWVIUVAWVJWVLVVRSWUSWOZWV
      JWURSJZIVVRUQWVMWVJWVNIVVRWVJVXHLZVXJSJZWUQSJZWVNWVHWVEVXHWVPWVCWVEVXHWVP
      WVHWVEVVRSVUGWOZVXHWVPSVVRVUGXMVOBVHXKZXLZVVRSVXGVUGXNZXOXPXQWVOVWTSJZWUP
      SJZWVQWVHWWBWVIVXHVWGUYSWWBVWGUYSLZVWTQJZWWBUYSVWGVWSQJZWWEUYSBQJWWFBUVBB
      UVCXRVURVWSUVDXSZVWTUVEXRXTZXEWVJVVRSWUOWOZVXHWWCWVJWUNSJZUKVVRUQWWIWVJWW
      JUKVVRWVJWUGLZWUMSJZWUNYAJWWJWWKWUKSJZWULSJWWLWWKWUJSJZWWBVWTUVHWKZWWMWVI
      WUGWWNWVHWVIWUGLWUHSJZWUISJZWWNWVCWUGWWPWVEWVCVVRSVUQWOWUGWWPSVVRVUQXMWVS
      XLVVRSWUFVUQXNXOXPWVEWUGWWQWVCWVEWVRWUGWWQWVTVVRSWUFVUGXNXOYBWUHWUIUVFVPX
      PWVHWWBWVIWUGWWHXEWVHWWOWVIWUGVWGUYSWWOWWDWWEWWOWWGVWTUVGXRXTXEWUJVWTUVIX
      HXIUVJUVKUVLWUKWULYCUVMWUMUVNWUNUVOYDWJUKUBVVRSWUNWUOWUOUMYEYFVVRSVXGWUOX
      NYGVWTWUPYHVPVXJWUQYCVPWJIUIVVRSWURWUSWUSUMYEYFWVJWVLWUSVVSJWVMWVJVVCVVSW
      USUYSVVCVVSPVWGWVIVVTXEXFSVVRWUSXMWVSXLYIYJUYSWVCVYRVWGWVEUYSVYRWVCWVDVMY
      KVUQVUSWUSVURVVCVWBUVPUVQYJUVTYOUVRUVSXRXQYLWUDWVAUJWUTVXSWUCWUTVUQUWAWIV
      PUJVUQVXSUWBUWCYMYNUWDYOYGUWEVWQVUJVYFVSVWGVWQVUJDVUIPZVYFVWQDVUEVUIVUADV
      UEPVWPVUADVVLVUEVWCVVMVQVGUWFVWQVYFWWRVUIVYDKZVUAVVNVVOLZVWPWWSVUAVVNVVOV
      VQVWAUWGWWTVWPLZVUIVYDDUWHZVYDVUHDVUSAVVCVUGVWBFUWIWXBVYDKWXAVYDDUWJVBWCY
      GDVUIVYDUWLUWKUWMVGVYFVYEVYADVYDVXTUWNUWOUWPVWRVVDHVXSVUPVXSJVXEVUPVXOPZL
      ZUGWRZVWRVVDVXRWXEUHVUPHUWQVXFVUPPZVXQWXDUGWXFVXPWXCVXEVXFVUPVXOUWRUWSUWT
      UXCVWRWXDVVDUGVWRWXDVVDVWRWXDLVXNVVCJZWXCVVDVWRVXEWXGWXCVWRVXELZWXGVVRSVX
      NWOZWXHVXMSJZIVVRUQWXIWXHWXJIVVRVWRVXEVXHWXJVWRVXEVXHLZLWVPVXLSJZWXJVWQVX
      HWVPVWGVXEVUAVWNVXHWVPVWOVUAVWNLWVRVXHWVPUYSUYTVWNWVRVYMUYSVYGWVRVYNVYGWV
      RUYSVYGWVRVYGWVEWVRWVGWVTUXAXJWDXSXAWWAYGXQYKVWRWWBVXKSJZWXLWXKVUAVWGWWBV
      WPUYSVWGWWBUYTWWHYBYBVXEVVRSVXDWOZVXHWXMVXEVXCSKWXNVXCYASVXCVXBUXDNYAVXBV
      XAUXBVXBUXEYPUXFYPVVRVXCSVXDUXGUXHVVRSVXGVXDXNYGVWTVXKYHUXIVXJVXLYCVPXAWJ
      IUIVVRSVXMVXNVXNUMYEYFVWQWXGWXIXDZVWGVXEUYSWXOUYTVWPUYSWXGVXNVVSJWXIUYSVV
      CVVSVXNVVTXFSVVRVXNXMWVSXLYIXEXEYJYLVWRVXEWXCXGVVBWXCUCVXNVVCVUQVXNPVVAVX
      OVUPVUQVXNVURVUTUXJUXKWIVPYMUXLUXMUXOUXNVXSRJZVYCVVGVXEUGWSZRJWXPVXCVVRVI
      OZWXQRVXCVVRUGVXBVXAVHXKWVSUYFVVRVXCUTZUYOZRJZWXRWXTKZWXRRJWXSRJZWYAVVRRJ
      VXCRJWYCVOBYQVXBVXAYQVVRVXCUXPYRWXSUXQUXRVVRYSJWYBWVSVXCVVRYSUXSYTWXTWXRU
      XTYRUYAVXEUHUGVXOUYBYTVVFVYCUBVXSRVUMVXSPZVUOVYAVVEVYBWYDVUNVXTDVUMVXSWFV
      LVVDHVUMVXSWGWHWIUYCUYDUYEYMUYGUYHUYIUYJUYKYNVUAVVNAVEJVUCVULXDVVQVVNAVVJ
      VEFVUSDUYLUYMGAVUEUAVUFUYPYDVUAVVNVVOVUBVVHXDVVQVWAUCUBVUSAVVCDHUFVWBFUYN
      VPUYQUYR $.
      $( [28-Feb-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    rrnheibor.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrnheibor.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    rrnheibor.3 $e |- T = ( MetOpen ` M ) $.
    rrnheibor.4 $e |- U = ( MetOpen ` ( Rn ` N ) ) $.
    $( Heine-Borel theorem for Euclidean space.  A subset of Euclidean space is
       compact iff it is closed and bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnheibor $p |- ( ( N e. NN /\ Y C_ X ) ->
                      ( T e. Comp <-> ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( wcel wss wa cms cfv cme adantr cdm eqid co ccomp ctotbnd ccld cbnd crrn
      rrnmet cxp cres metres syl5eqel syl biantrurd heibor syl6bb eleq1i rrncms
      cn wb cr cfz rrndm syl6reqr sseq2d biimpa cmsss syl11anc syl5bb rrntotbnd
      c1 cmap anbi12d bitrd ) DUQKZFELZMZAUAKZCNKZCUBKZMZFBUCOKZCUDKZMVOVPCPKZV
      PMVSVOWBVPVMWBVNVMDUEOZPKZWBDUFWDCWCFFUGUHZPHWCFUIUJUKQULCACRRZIWFSUMUNVO
      VQVTVRWAVQWENKZVOVTCWENHUOVOWCNKZFWCRRZLZWGVTURVMWHVNDUPQVMVNWJVMEWIFVMWI
      USVIDUTTVJTEDVAGVBVCVDWCBWIFWISJVEVFVGCDEFGHVHVKVL $.
      $( [2-Sep-2009] $)
  $}

