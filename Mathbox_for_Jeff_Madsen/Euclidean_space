$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Euclidean space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The real numbers are a complete metric space.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    recms $p |- ( ( abs o. - ) |` ( RR X. RR ) ) e. CMet $=
      ( vx vy cmin cr wcel cfv cc wss co wa wb cc0 3syl wceq syl wbr cle adantr
      c2 cexp cabs ccom cxp cres cms copn ccld cdif cbl crp wrex wral cme cnmet
      cv eqid cnmetba isopn4 ax-mp difss cim wne eldifi imcl recn eldifn reim0b
      wn necon3bbid mpbid absrpcl syl2anc a1i blssm syl3anc sseld clt cre caddc
      elbl subcl syl2an recl sqge0 resqcl addge02 absresq absvalsq2 imsub reim0
      adantl opreq2d subid1 3eqtrd opreq1d eqtrd 3brtr4d absge0 syl22anc mpbird
      abscl le2sq cnmetdval breq2d metcl lenlt bitr3d con2d adantld sylbid jcad
      ex eldif syl6ibr ssrdv opreq2 sseq1d rcla4ev rgen mpbir2an ctop ax-resscn
      opntop cuni uniopn2 iscld2 mp2an mpbir cncms cmsss ) UACUBZDDUCUDUEEZDYKU
      FFZUGFEZYNGDUHZYMEZYPYOGHZAUOZBUOZYKUIFZIZYOHZBUJUKZAYOULZYKUMEZYPYQUUDJK
      YKYKUPZUNZABYOYKYMGYKUUFUQZYMUPZURUSGDUTUUCAYOYRYOEZYRVAFZUAFZUJEZYRUULYT
      IZYOHZUUCUUJUUKGEZUUKLVBZUUMUUJYRGEZUUKDEZUUPYRGDVCZYRVDZUUKVEZMZUUJYRDEZ
      VHUUQYRGDVFUUJUVDUUKLUUJUURUVDUUKLNKUUTYRVGOVIVJUUKVKVLZUUJBUUNYOUUJYSUUN
      EZYSGEZYSDEZVHZJYSYOEUUJUVFUVGUVIUUJUUNGYSUUJUUEUURUUMUUNGHUUEUUJUUGVMZUU
      TUVEYKYRUULGUUHVNVOVPUUJUVFUVGYRYSYKIZUULVQPZJZUVIUUJUUEUURUUMUVFUVMKUVJU
      UTUVEYSYKYRUULGUUHVTVOUUJUVLUVIUVGUUJUVHUVLUUJUVHUVLVHZUUJUVHJZUULYRYSCIZ
      UAFZQPZUVNUVOUVRUULSTIZUVQSTIZQPZUVOUUKSTIZUVPVRFZSTIZUWBVSIZUVSUVTQUVOLU
      WDQPZUWBUWEQPZUVOUVPGEZUWCDEZUWFUUJUURUVGUWHUVHUUTYSVEZYRYSWAWBZUVPWCZUWC
      WDMUVOUWBDEZUWDDEZUWFUWGKUVOUUSUWMUUJUUSUVHUUJUURUUSUUTUVAORZUUKWEOUVOUWH
      UWIUWNUWKUWLUWCWEMUWBUWDWFVLVJUVOUUSUVSUWBNUWOUUKWGOUVOUVTUWDUVPVAFZSTIZV
      SIZUWEUVOUWHUVTUWRNUWKUVPWHOUVOUWQUWBUWDVSUVOUWPUUKSTUVOUWPUUKYSVAFZCIZUU
      KLCIZUUKUUJUURUVGUWPUWTNUVHUUTUWJYRYSWIWBUVOUWSLUUKCUVHUWSLNUUJYSWJWKWLUV
      OUUPUXAUUKNUUJUUPUVHUVCRUUKWMOWNWOWLWPWQUVOUULDEZLUULQPZUVQDEZLUVQQPZUVRU
      WAKUUJUXBUVHUUJUURUUSUXBUUTUVAUUSUUPUXBUVBUUKXAOMRZUUJUXCUVHUUJUUPUXCUVCU
      UKWRORUVOUWHUXDUWKUVPXAOUVOUWHUXEUWKUVPWROUULUVQXBWSWTUVOUULUVKQPZUVRUVNU
      VOUVKUVQUULQUUJUURUVGUVKUVQNUVHUUTUWJYRYSYKUUFXCWBXDUVOUXBUVKDEZUXGUVNKUX
      FUVOUUEUURUVGUXHUUEUVOUUGVMUUJUURUVHUUTRUVHUVGUUJUWJWKYRYSYKGUUHXEVOUULUV
      KXFVLXGVJXLXHXIXJXKYSGDXMXNXOUUBUUOBUULUJYSUULNUUAUUNYOYSUULYRYTXPXQXRVLX
      SXTYMYAEZDGHZYNYPKUUEUXIUUGYKYMUUIYCUSYBDYMGUUEGYMYDNUUGYKYMGUUHUUIYEUSYF
      YGYHYKUEEUXJYLYNKYKUUFYIYBYKYMGDUUHUUIYJYGYH $.
      $( [2-Sep-2009] $)
  $}

  $c Rn $.
  $( Extend class notation with the n-dimensional Euclidean space. $)
  crrn $a class Rn $.

  ${
    $d n r x y d k $.
    $( Define n-dimensional Euclidean space as a metric space with the standard
       Euclidean norm given by the quadratic mean. $)
    df-rrn $a |- Rn = { <. n , r >. | ( n e. NN /\ r = { <. <. x , y >. , d >.
          | ( ( x e. ( RR ^m ( 1 ... n ) ) /\ y e. ( RR ^m ( 1 ... n ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... n )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) } $.
  $}

  ${
    $d N n r x y d k $.
    $( The n-dimensional Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnval $p |- ( N e. NN -> ( Rn ` N ) = { <. <. x , y >. , d >. |
            ( ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... N )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) $=
      ( vn vr cv cr c1 cfz co cmap wcel wa cfv csu csqr wceq copab2 cmin opreq2
      c2 cexp crrn opreq2d eleq2d anbi12d sumeq1d fveq2d eqeq2d oprabbidv oprex
      cn df-rrn eqid oprabex2 fvopab4 ) FGDAHZIJFHZKLZMLZNZBHZVBNZOZEHZVACHZUSP
      VHVDPUALUCUDLZCQZRPZSZOZABETUSIJDKLZMLZNZVDVONZOZVGVNVICQZRPZSZOZABETZUNU
      EUTDSZVMWBABEWDVFVRVLWAWDVCVPVEVQWDVBVOUSWDVAVNIMUTDJKUBZUFZUGWDVBVOVDWFU
      GUHWDVKVTVGWDVJVSRWDVAVNVICWEUIUJUKUHULABCFGEUOABEVOVOVTWCIVNMUMZWGWCUPUQ
      UR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.  $d X x y d k $.  $d Y x y d k $.
    $( The value of the Euclidean metric.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnmval $p |- ( ( N e. NN /\ X e. ( RR ^m ( 1 ... N ) )
                  /\ Y e. ( RR ^m ( 1 ... N ) ) ) -> ( X ( Rn ` N ) Y ) =
        ( sqr ` sum_ k e. ( 1 ... N ) ( ( ( X ` k ) - ( Y ` k ) ) ^ 2 ) ) ) $=
      ( vx vy vd wcel co cfv cv cmin c2 cexp csu csqr wceq wa fveq1 opreq1d cfz
      cn cmap crrn copab2 rrnval opreqd fvex sumeq2sdv fveq2d opreq2d oprabval2
      cr c1 eqid sylan9eq 3impb ) BUBHZCUMUNBUAIZUCIZHZDUTHZCDBUDJZIZUSAKZCJZVE
      DJZLIZMNIZAOZPJZQURVAVBRVDCDEKZUTHFKZUTHRGKUSVEVLJZVEVMJZLIZMNIZAOZPJZQRE
      FGUEZIVKURVCVTCDEFABGUFUGEFGCDUTUTVSVKVTUSVFVOLIZMNIZAOZPJVJPUHVLCQZVRWCP
      WDUSVQWBAWDVPWAMNWDVNVFVOLVEVLCSTTUIUJVMDQZWCVJPWEUSWBVIAWEWAVHMNWEVOVGVF
      LVEVMDSUKTUIUJVTUOULUPUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.
    $( The underlying set of Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrndm $p |- ( N e. NN -> dom dom ( Rn ` N ) = ( RR ^m ( 1 ... N ) ) ) $=
      ( vx vy vd vk wcel cfv cdm cr c1 cfz co cv wa csqr wceq dmeqd c0 wne wn
      cn crrn cmap cxp cmin c2 cexp csu copab2 rrnval fvex eqid dmoprab2 syl6eq
      cc0 0re n0i ax-mp reex oprex map0 simplbi mto df-ne mpbir a1i dmxp eqtrd
      syl ) AUAFZAUBGZHZHIJAKLZUCLZVNUDZHZVNVJVLVOVJVLBMZVNFCMZVNFNDMVMEMZVQGVS
      VRGUELUFUGLEUHZOGZPNBCDUIZHVOVJVKWBBCEADUJQBCDVNVNWAWBVTOUKWBULUMUNQVJVNR
      SZVPVNPWCVJWCVNRPZTWDIRPZUOIFWETUPIUOUQURWDWEVMRSIVMUSJAKUTVAVBVCVNRVDVEV
      FVNVNVGVIVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k z m n $.
    $( Euclidean space is a metric space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnmet $p |- ( N e. NN -> ( Rn ` N ) e. Met ) $=
      ( vx vy vk vm wcel cfv cr wf co wceq caddc wral wa cmin cexp csqr syl2anc
      cc0 c2 vz vd vn cn crrn cdm cxp cv wb cle wbr cme cfz cmap csu copab2 w3a
      c1 cuz elnnuz biimpi 3ad2ant1 reex oprex elmap sylanb 3ad2antl2 3ad2antl3
      ffvelrn resubcl resqcl syl ralrimiva fsumrecl jca fsumcmp0 resqrcl 3expib
      sqge0 ralrimivv eqid foprab2 sylib rrnval feq1d mpbird rrndm feq2d eleq2d
      xpeq12d anbi12d rrnmval eqeq1d sqr00 fsum00 recn sqeq0 recnd subeq0 bitrd
      3syl ralbidva wfn ffn sylbi biantrurd eqfnfv bitr4d syl2an 3adant1 3bitrd
      cc eqidd biimpd 3anbi123i copab simpl 3ad2antl1 fopab2 adantl trirn fveq2
      wi syl3anc opreq12d fvopab4 npncan adantll eqtr2d opreq1d sumeq2dv fveq2d
      cneg negsubdi2 subcl eqcomd 3brtr4d 3expb ancom2s cvv sqneg 3eqtr3d 3exp2
      sylan2b 3adant3r3 3adantr2 3adantr1 3imp syld ralrimiv sylbid ismet ax-mp
      fvex sylanbrc ) AUDFZAUEGZUFUFZUURUGZHUUQIZBUHZCUHZUUQJZSKZUVAUVBKZUIZUVC
      UAUHZUVAUUQJZUVGUVBUUQJZLJZUJUKZUAUURMZNZCUURMBUURMZUUQULFZUUPUUTHURAUMJZ
      UNJZUVQUGZHUUQIZUUPUVSUVRHUVAUVQFZUVBUVQFZNZUBUHUVPDUHZUVAGZUWCUVBGZOJZTP
      JZDUOZQGZKNBCUBUPZIZUUPUWIHFZCUVQMBUVQMUWKUUPUWLBCUVQUVQUUPUVTUWAUWLUUPUV
      TUWAUQZUWHHFZSUWHUJUKZUWLUWMAURUSGFZUWGHFZDUVPMUWNUUPUVTUWPUWAUUPUWPAUTVA
      VBZUWMUWQDUVPUWMUWCUVPFZNZUWFHFZUWQUWTUWDHFZUWEHFZUXAUVTUUPUWSUXBUWAUVTUV
      PHUVAIZUWSUXBHUVPUVAVCURAUMVDZVEZUVPHUWCUVAVIZVFVGZUWAUUPUWSUXCUVTUWAUVPH
      UVBIZUWSUXCHUVPUVBVCUXEVEZUVPHUWCUVBVIZVFVHZUWDUWEVJRZUWFVKVLZVMUWGDURAVN
      RZUWMUWPUWQSUWGUJUKZNZDUVPMZUWOUWRUWMUXQDUVPUWTUWQUXPUXNUWTUXAUXPUXMUWFVS
      VLVOVMZUWGDURAVPRZUWHVQRVRVTBCUBUVQUVQUWIHUWJUWJWAWBWCUUPUVRHUUQUWJBCDAUB
      WDWEWFUUPUUSUVRHUUQUUPUURUVQUURUVQAWGZUYAWJWHWFUUPUVMBCUURUURUUPUVAUURFZU
      VBUURFZNUWBUVMUUPUYBUVTUYCUWAUUPUURUVQUVAUYAWIUUPUURUVQUVBUYAWIWKUUPUVTUW
      AUVMUWMUVFUVLUWMUVDUWISKZUWHSKZUVEUWMUVCUWISDAUVAUVBWLZWMUWMUWNUWOUYDUYEU
      IUXOUXTUWHWNRUWMUYEUWGSKZDUVPMZUWDUWEKZDUVPMZUVEUWMUWPUXRUYEUYHUIUWRUXSUW
      GDURAWORUWMUYGUYIDUVPUWTUYGUWFSKZUYIUWTUXAUWFXLFUYGUYKUIUXMUWFWPUWFWQXAUW
      TUWDXLFZUWEXLFZUYKUYIUIUWTUWDUXHWRUWTUWEUXLWRUWDUWEWSRWTXBUVTUWAUYJUVEUIZ
      UUPUVTUVAUVPXCZUVBUVPXCZUYNUWAUVTUXDUYOUXFUVPHUVAXDXEUWAUXIUYPUXJUVPHUVBX
      DXEUYOUYPNZUYJUVPUVPKZUYJNUVEUYQUYRUYJUYQUVPXMXFDUVPUVPUVAUVBXGXHXIXJXKXK
      UWMUVKUAUURUWMUVGUURFZUVGUVQFZUVKUUPUVTUYSUYTYCUWAUUPUYSUYTUUPUURUVQUVGUY
      AWIXNVBUUPUVTUWAUYTUVKYCUUPUVTUWAUYTUVKUUPUVTUWAUYTUQZNZUWIUVPUWCUVGGZUWD
      OJZTPJZDUOZQGZUVPVUCUWEOJZTPJZDUOZQGZLJZUVCUVJUJVUAUUPUXDUXIUVPHUVGIZUQZU
      WIVULUJUKUVTUXDUWAUXIUYTVUMUXFUXJHUVPUVGVCUXEVEXOUUPVUNNZUVPUWCEUHZUVPFZU
      CUHZVUPUVAGZVUPUVGGZOJZKNEUCXPZGZUWCVUQVURVUTVUPUVBGZOJZKNEUCXPZGZLJZTPJZ
      DUOZQGZUVPVVCTPJZDUOZQGZUVPVVGTPJZDUOZQGZLJZUWIVULUJVUOUUPUVPHVVBIZUVPHVV
      FIZVVKVVRUJUKUUPVUNXQVUNVVSUUPVUNVVAHFZEUVPMVVSVUNVWAEUVPVUNVUQNZVUSHFZVU
      THFZVWAUXDUXIVUQVWCVUMUVPHVUPUVAVIXRVUMUXDVUQVWDUXIUVPHVUPUVGVIVHZVUSVUTV
      JRVMEUCUVPHVVAVVBVVBWAZXSWCXTVUNVVTUUPVUNVVEHFZEUVPMVVTVUNVWGEUVPVWBVWDVV
      DHFZVWGVWEUXIUXDVUQVWHVUMUVPHVUPUVBVIVGVUTVVDVJRVMEUCUVPHVVEVVFVVFWAZXSWC
      XTDAVVBVVFYAYDVUOUWHVVJQVUOUVPUWGVVIDVUOUWSNZUWFVVHTPVWJVVHUWDVUCOJZVUHLJ
      ZUWFUWSVVHVWLKVUOUWSVVCVWKVVGVUHLEUCUWCVVAVWKUVPVVBVUPUWCKZVUSUWDVUTVUCOV
      UPUWCUVAYBVUPUWCUVGYBZYEVWFUWDVUCOVDYFZEUCUWCVVEVUHUVPVVFVWMVUTVUCVVDUWEO
      VWNVUPUWCUVBYBYEVWIVUCUWEOVDYFZYEXTVUNUWSVWLUWFKZUUPVUNUWSNZUYLVUCXLFZUYM
      VWQVWRUWDUXDUXIUWSUXBVUMUXGXRWRZVWRVUCVUMUXDUWSVUCHFUXIUVPHUWCUVGVIVHWRZV
      WRUWEUXIUXDUWSUXCVUMUXKVGWRUWDVUCUWEYGYDYHYIYJYKYLVUOVUGVVNVUKVVQLVUOVUFV
      VMQVUOUVPVUEVVLDVUNUWSVUEVVLKUUPVWRVUDYMZTPJZVWKTPJVUEVVLVWRVXBVWKTPVWRVW
      SUYLVXBVWKKVXAVWTVUCUWDYNRYJVWRVUDXLFZVXCVUEKVWRVWSUYLVXDVXAVWTVUCUWDYORV
      UDUUAVLVWRVWKVVCTPUWSVWKVVCKVUNUWSVVCVWKVWOYPXTYJUUBYHYKYLVUOVUJVVPQVUOUV
      PVUIVVODVWJVUHVVGTPUWSVUHVVGKVUOUWSVVGVUHVWPYPXTYJYKYLYEYQUUDUUPUVTUWAUVC
      UWIKUYTUYFUUEVUBUVHVUGUVIVUKLUUPUVTUYTUVHVUGKZUWAUUPUYTUVTVXEUUPUYTUVTVXE
      DAUVGUVAWLYRYSUUFUUPUWAUYTUVIVUKKZUVTUUPUYTUWAVXFUUPUYTUWAVXFDAUVGUVBWLYR
      YSUUGYEYQUUCUUHUUIUUJVOVRUUKVTUUQYTFUVOUUTUVNNUIAUEUUNBCUAYTUUQUURUURWAUU
      LUUMUUO $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M k n $.  $d X k n $.  $d N k n $.  $d A k $.  $d F k n $.  $d G k n $.
    $d R k n $.
    rrndstprj1.1 $e |- M = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    rrndstprj1.2 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    $( The distance between two points in Euclidean space is greater than the
       distance between the projections onto one coordinate.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj1 $p |- ( ( ( N e. NN /\ A e. ( 1 ... N ) ) /\ ( F e. X /\ G e. X
                    ) ) -> ( ( F ` A ) M ( G ` A ) ) <_ ( F ( Rn ` N ) G ) ) $=
      ( vk wcel c1 co wa cfv c2 cexp cle wbr cr syl2anc cn cfz cmin cabs cv csu
      csqr crrn cuz wral elfzuz2 adantr wf cmap eleq2i reex oprex elmap ffvelrn
      cc0 bitri sylanb adantlr adantll resubcl resqcl syl sqge0 jca simplr wceq
      ralrimiva fveq2 opreq12d opreq1d fsumlt1 syl3anc ancoms sylan2b ad2ant2lr
      ad2ant2l absresq fsumrecl fsumcmp0 sqsqr 3brtr4d wb cc recn abscl resqrcl
      3syl absge0 sqrge0 le2sq syl22anc mpbird remetdval simpll biimpi ad2antrl
      ad2antll rrnmval ) EUAJZAKEUBLZJZMZBFJZCFJZMZMZABNZACNZUCLZUDNZXEIUEZBNZX
      PCNZUCLZOPLZIUFZUGNZXLXMDLZBCEUHNLZQXKXOYBQRZXOOPLZYBOPLZQRZXKXNOPLZYAYFY
      GQXKEKUINJZXTSJZUTXTQRZMZIXEUJZXFYIYAQRXGYJXJUAAKEUKULZXKYMIXEXKXPXEJZMZY
      KYLYQXSSJZYKYQXQSJZXRSJZYRXJYPYSXGXHYPYSXIXHXESBUMZYPYSXHBSXEUNLZJZUUAFUU
      BBHUOZSXEBUPKEUBUQZURVAZXESXPBUSVBVCVDXJYPYTXGXIYPYTXHXIXESCUMZYPYTXICUUB
      JZUUGFUUBCHUOZSXECUPUUEURVAZXESXPCUSVBVDVDXQXRVETZXSVFVGZYQYRYLUUKXSVHVGV
      IVLZXDXFXJVJXTYIIAKEXPAVKZXSXNOPUUNXQXLXRXMUCXPABVMXPACVMVNVOVPVQXKXNSJZY
      FYIVKXKXLSJZXMSJZUUOXFXHUUPXDXIXHXFUUAUUPUUFUUAXFUUPXESABUSVRVSVTZXFXIUUQ
      XDXHXIXFUUGUUQUUJUUGXFUUQXESACUSVRVSWAZXLXMVETZXNWBVGXKYASJZUTYAQRZYGYAVK
      XKYJYKIXEUJUVAYOXKYKIXEUULVLXTIKEWCTZXKYJYNUVBYOUUMXTIKEWDTZYAWETWFXKXOSJ
      ZUTXOQRZYBSJZUTYBQRZYEYHWGXKUUOXNWHJZUVEUUTXNWIZXNWJWLXKUUOUVIUVFUUTUVJXN
      WMWLXKUVAUVBUVGUVCUVDYAWKTXKUVAUVBUVHUVCUVDYAWNTXOYBWOWPWQXKUUPUUQYCXOVKU
      URUUSXLXMDGWRTXKXDUUCUUHYDYBVKXDXFXJWSXHUUCXGXIXHUUCUUDWTXAXIUUHXGXHXIUUH
      UUIWTXBIEBCXCVQWF $.
      $( [2-Sep-2009] $)

    $( Bound on the distance between two points in Euclidean space given bounds
       on the distances in each coordinate.  This theorem and ~ rrndstprj1 can
       be used to show that the supremum norm and Euclidean norm are
       equivalent.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj2 $p |- ( ( ( N e. NN /\ F e. X /\ G e. X ) /\ ( R e. RR+ /\
                    A. n e. ( 1 ... N ) ( ( F ` n ) M ( G ` n ) ) < R ) ) ->
                                ( F ( Rn ` N ) G ) < ( R x. ( sqr ` N ) ) ) $=
      ( vk wcel cr c1 co clt wbr wa c2 wceq syl2anc cn w3a cfz cmap crp cv wral
      cfv crrn csqr cmul biid eleq2i 3anbi123i cmin cexp csu rrnmval adantr cuz
      elnnuz biimpi 3ad2ant1 wf oprex elmap ffvelrn 3ad2antl2 3ad2antl3 resubcl
      reex sylanb resqcl syl adantlr rpre ad2antlr fveq2 opreq12d breq1d rcla4v
      wi imdistani ancoms cabs remetdval opreq1d absresq eqtrd ad2ant2r cc0 cle
      wb cme remet a1i remetba metcl syl3anc metge0 rpge0 lt2sq syl22anc biimpa
      anasss eqbrtrrd sylan2 expr 3jca ralrimiva fsumlt caddc cc rpcn fsumconst
      imp sqcl syl2an nncn ax-1cn npcan sylancl 3ad2antl1 adantrr breqtrd sqge0
      fsumrecl jca fsumcmp0 sqsqr nnre nnnn0 nn0ge0 resqrcl recnd sqmul 3eqtr3d
      cn0 mulcom sqrge0 3brtr4d remulcl mulge0 mpbird eqbrtrd ) FUAKZCGKZDGKZUB
      UUFCLMFUCNZUDNZKZDUUJKZUBZAUEKZBUFZCUHZUUODUHZENZAOPZBUUIUGZQZCDFUIUHNZAF
      UJUHZUKNZOPUUFUUFUUGUUKUUHUULUUFULGUUJCIUMGUUJDIUMUNUUMUVAQZUVBUUIJUFZCUH
      ZUVFDUHZUONZRUPNZJUQZUJUHZUVDOUUMUVBUVLSUVAJFCDURUSUVEUVLUVDOPZUVLRUPNZUV
      DRUPNZOPZUVEUVKFARUPNZUKNZUVNUVOOUVEUVKUUIUVQJUQZUVROUVEFMUTUHKZUVJLKZUVQ
      LKZUVJUVQOPZUBZJUUIUGUVKUVSOPUUMUVTUVAUUFUUKUVTUULUUFUVTFVAVBZVCZUSUVEUWD
      JUUIUVEUVFUUIKZQUWAUWBUWCUUMUWGUWAUVAUUMUWGQZUVILKZUWAUWHUVGLKZUVHLKZUWIU
      UKUUFUWGUWJUULUUKUUILCVDUWGUWJLUUICVKMFUCVEZVFUUILUVFCVGVLVHZUULUUFUWGUWK
      UUKUULUUILDVDUWGUWKLUUIDVKUWLVFUUILUVFDVGVLVIZUVGUVHVJTZUVIVMVNZVOUVAUWBU
      UMUWGUUNUWBUUTUUNALKZUWBAVPZAVMVNUSVQUVEUWGUWCUUMUUNUUTUWGUWCWBUUMUUNQZUU
      TUWGUWCUUTUWGQUWSUWGUVGUVHENZAOPZQZUWCUWGUUTUXBUWGUUTUXAUUSUXABUVFUUIUUOU
      VFSZUURUWTAOUXCUUPUVGUUQUVHEUUOUVFCVRUUOUVFDVRVSVTWAWCWDUWSUXBQUWTRUPNZUV
      JUVQOUUMUWGUXDUVJSUUNUXAUWHUXDUVIWEUHZRUPNZUVJUWHUWTUXERUPUWHUWJUWKUWTUXE
      SUWMUWNUVGUVHEHWFTWGUWHUWIUXFUVJSUWOUVIWHVNWIWJUWSUWGUXAUXDUVQOPZUWSUWGQZ
      UXAUXGUXHUWTLKZWKUWTWLPZUWQWKAWLPZUXAUXGWMUXHEWNKZUWJUWKUXIUXLUXHEHWOWPZU
      UMUWGUWJUUNUWMVOZUUMUWGUWKUUNUWNVOZUVGUVHELEHWQZWRWSUXHUXLUWJUWKUXJUXMUXN
      UXOUVGUVHELUXPWTWSUUNUWQUUMUWGUWRVQUUNUXKUUMUWGAXAZVQUWTAXBXCXDXEXFXGXHXE
      XPXIXJUVJUVQJMFXKTUUMUUNUVSUVRSZUUTUUFUUKUUNUXRUULUUFUUNQZUVSFMUONMXLNZUV
      QUKNZUVRUUFUVTUVQXMKZUVSUYASUUNUWEUUNAXMKZUYBAXNZAXQVNUVQJMFXOXRUXSUXTFUV
      QUKUUFUXTFSZUUNUUFFXMKMXMKUYEFXSXTFMYAYBUSWGWIYCYDYEUUMUVNUVKSZUVAUUMUVKL
      KZWKUVKWLPZUYFUUMUVTUWAJUUIUGUYGUWFUUMUWAJUUIUWPXJUVJJMFYGTZUUMUVTUWAWKUV
      JWLPZQZJUUIUGUYHUWFUUMUYKJUUIUWHUWAUYJUWPUWHUWIUYJUWOUVIYFVNYHXJUVJJMFYIT
      ZUVKYJTUSUUMUUNUVOUVRSZUUTUUFUUKUUNUYMUULUXSUVCAUKNZRUPNZUVCRUPNZUVQUKNZU
      VOUVRUUFUVCXMKZUYCUYOUYQSUUNUUFUVCUUFFLKZWKFWLPZUVCLKZFYKZUUFFYRKUYTFYLFY
      MVNZFYNTZYOZUYDUVCAYPXRUXSUYNUVDRUPUUFUYRUYCUYNUVDSUUNVUEUYDUVCAYSXRWGUXS
      UYPFUVQUKUUFUYPFSZUUNUUFUYSUYTVUFVUBVUCFYJTUSWGYQYCYDUUAUUMUUNUVMUVPWMZUU
      TUWSUVLLKZWKUVLWLPZUVDLKZWKUVDWLPZVUGUUMVUHUUNUUMUYGUYHVUHUYIUYLUVKYNTUSU
      UMVUIUUNUUMUYGUYHVUIUYIUYLUVKYTTUSUUFUUKUUNVUJUULUUNUUFVUJUUNUWQVUAVUJUUF
      UWRVUDAUVCUUBXRWDYCUUFUUKUUNVUKUULUUNUUFVUKUUNUWQUXKQVUAWKUVCWLPZQVUKUUFU
      UNUWQUXKUWRUXQYHUUFVUAVULVUDUUFUYSUYTVULVUBVUCFYTTYHAUVCUUCXRWDYCUVLUVDXB
      XCYDUUDUUEVL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N f g j k m n r s x y z $.
    $( Euclidean space is complete.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrncms $p |- ( N e. NN -> ( Rn ` N ) e. CMet ) $=
      ( vn vj vk vr cn wcel cfv cv wbr wrex wi wral wa co wceq clt crp adantlr
      cr vf vx vm vz vy vg vs crrn cme cdm wf clm cca cms rrnmet wex copab cabs
      cfz cmin ccom cxp cres cfn fzfi cle ffvelrn adantrr adantrl adantll rrndm
      c1 jca cmap adantr eleq2d anbi12d imdistani eqid rrndstprj1 adantlrl reex
      biimpd syl oprex elmap syl6bb expcom sylan9 imp remet remetba metcl 3expb
      mp3an1 syl2anc sylan rpre ad2antlr lelttr syl3anc mpand syldan fveq2 fvex
      anassrs fveq1d fvopab4 adantl opreq12d breq1d imim2d ralimdva reximdva wb
      sylibrd simprl iscau5 syl6rbb biimpar an32s sylan2 ralrimiva anasss sylib
      ancom2s fopab2 sylancr 3imtr4d eqcomi a1i breq2 ad2antrr adantllr lmbrnns
      wne 3syl ex cc cc0 breq1 rexeqbidv recms iscms mpbi simpri vtoclri ac6sfi
      biimprd csqr cdiv biimpa eqcomd nnrp rpsqrcl rpdivcl ancoms imbi2d rcla4v
      rexralbidv sylbid c0 wss cuz elnnuz biimpi eluzfz1 ne0i ad3antrrr filbcmb
      nnssre w3a cmul 3ad2ant1 rrndstprj2 exp32 syld imp32 rpcn nnre cn0 nn0ge0
      nnnn0 resqrcl recn rpne0 divcan1 3ad2antl1 breqtrd expdimp an4s ralrimdva
      reximdv 3syld eqidd syl3an1 3expa imdistand syland eximdv df-rex sylanbrc
      mpd sylibr iscms2 ) AFGZAUHHZUIGZFUXGUJUJZUAIZUKZUXJUBIZUXGULHJZUBUXIKZLZ
      UAUXGUMHZMUXGUNGAUOZUXFUXOUAUXPUXFUXJUXPGZNZUXKUXNUXSUXKNZUXLUXIGZUXMNZUB
      UPZUXNUXTVLAUSOZTUXLUKZUCIZFGZUDIBIZUYFUXJHZHZPNUCUDUQZUYHUXLHZURUTVATTVB
      VCZULHZJZBUYDMZNZUBUPZUYCUXTUYDVDGZUYKUEIZUYNJZUETKZBUYDMUYRVLAVEZUXTVUBB
      UYDUXTUYHUYDGZNUYKUYMUMHZGZVUBUXSUXKVUDVUFUXFUXKVUDNZUXRVUFUXFVUGNZUXRVUF
      VUHCIZDIZVFJZVUIUXJHZVUJUXJHZUXGOZEIZQJZLZDFMZCFKZERMZVUKVUIUYKHZVUJUYKHZ
      UYMOZVUOQJZLZDFMZCFKZERMZUXRVUFVUHVUSVVGERVUHVUORGZNZVURVVFCFVVJVUIFGZNZV
      UQVVEDFVVLVUJFGZNZVUPVVDVUKVVNVUPUYHVULHZUYHVUMHZUYMOZVUOQJZVVDVVJVVKVVMV
      UPVVRLZVVJVVKVVMNZVULUXIGZVUMUXIGZNZVVSVUHVVTVWCVVIVUGVVTVWCUXFUXKVVTVWCV
      UDUXKVVTNVWAVWBUXKVVKVWAVVMFUXIVUIUXJVGVHUXKVVMVWBVVKFUXIVUJUXJVGZVIVMSVJ
      SVVJVWCNZVVQVUNVFJZVUPVVRVUHVWCVWFVVIUXFVUDVWCVWFUXKUXFVUDNZVWCNZVWGVULTU
      YDVNOZGZVUMVWIGZNZNVWFVWGVWCVWLVWGVWCVWLVWGVWAVWJVWBVWKVWGUXIVWIVULUXFUXI
      VWIPZVUDAVKZVOZVPVWGUXIVWIVUMVWOVPVQWCVRUYHVULVUMUYMAVWIUYMVSZVWIVSZVTWDW
      ASVWEVVQTGZVUNTGZVUOTGZVWFVUPNVVRLVUHVWCVWRVVIUXFVUDVWCVWRUXKVWHVVOTGZVVP
      TGZVWRVWGVWAVXAVWBVWGVWAVXAUXFVWAUYDTVULUKZVUDVXAUXFVWAVXCUXFVWAVWJVXCUXF
      UXIVWIVULVWNVPTUYDVULWBVLAUSWEZWFWGWCVXCVUDVXAUYDTUYHVULVGWHWIWJVHVWGVWBV
      XBVWAVWGVWBVXBUXFVWBUYDTVUMUKZVUDVXBUXFVWBVXEUXFVWBVWKVXEUXFUXIVWIVUMVWNV
      PZTUYDVUMWBVXDWFWGWCVXEVUDVXBUYDTUYHVUMVGWHWIWJVIUYMUIGZVXAVXBVWRUYMVWPWK
      ZVVOVVPUYMTUYMVWPWLZWMWOWPWASVUHVWCVWSVVIUXFVWCVWSVUGUXFUXHVWCVWSUXQUXHVW
      AVWBVWSVULVUMUXGUXIUXIVSZWMWNWQSSVVIVWTVUHVWCVUOWRWSVVQVUNVUOWTXAXBXCXFVV
      NVVCVVQVUOQVVNVVAVVOVVBVVPUYMVVKVVAVVOPVVJVVMUCUDVUIUYJVVOFUYKUYFVUIPUYHU
      YIVULUYFVUIUXJXDXGUYKVSZUYHVULXEXHWSVVMVVBVVPPVVLUCUDVUJUYJVVPFUYKUYFVUJP
      UYHUYIVUMUYFVUJUXJXDXGVXKUYHVUMXEXHZXIXJXKXPXLXMXNXMVUHUXHUXKUXRVUTXOUXFU
      XHVUGUXQVOUXFUXKVUDXQEUXGCDUXJUXIVXJXRWPVUHVXGFTUYKUKZVUFVVHXOVXHVUHUYJTG
      ZUCFMZVXMUXFVUDUXKVXOUXFVUDUXKVXOVWGUXKNVXNUCFVWGUXKUYGVXNUXKUYGNZVWGUYIU
      XIGZVXNFUXIUYFUXJVGZUXFVXQVUDVXNUXFVXQNUYDTUYIUKZVUDVXNUXFVXSVXQUXFVXQUYI
      VWIGVXSUXFUXIVWIUYIVWNVPTUYDUYIWBVXDWFXSXTZUYDTUYHUYIVGZWQYAYBXFYCYDYFUCU
      DFTUYJUYKVXKYGZYEEUYMCDUYKTVXIXRYHYIWJYAXFUFIZUYTUYNJZUEUYMUJUJZKZVUBUFUY
      KVUEVYCUYKPZVYDVUAUEVYETVYETPVYGTVYEVXIYJYKVYCUYKUYTUYNUUAUUBVXGVYFUFVUEM
      ZUYMUNGVXGVYHNUUCUEUYMUFVYEVYEVSUUDUUEUUFUUGWDYCVUAUYOBUEUYDTUBUYTUYLUYKU
      YNYLUUHYHUXTUYQUYBUBUXTUYEUYAUYPUYBUXTUYAUYEUXTUYAUXLVWIGZUYEUXTUXIVWIUXL
      UXFVWMUXRUXKVWNYMVPTUYDUXLWBVXDWFZWGUUIUXTUYAUYPUXMUXFUXKUYAUYPUXMLZLUXRU
      XFUXKNZUYAVYKVYLUYANZUYPVUKVUMUXLUXGOZVUOQJZLZDFMZCFKZERMZUXMVYMUYPVYRERV
      YMVVINZUYPVUKVVPUYLUYMOZVUOAUUJHZUUKOZQJZLZDFMCFKZBUYDMZVUKWUDBUYDMZLZDFM
      ZCFKZVYRVYTUYOWUFBUYDVYTVUDNZUYOVUKWUAUGIZQJZLZDFMCFKZUGRMZWUFWULVXGUYLTG
      ZVXMUYOWUQXOVXGWULVXHYKVYMVUDWURVVIUXFUYAVUDWURUXKUXFUYANZUYEVUDWURWUSVYI
      UYEUXFUYAVYIUXFUXIVWIUXLVWNVPZUULVYJYEUYDTUYHUXLVGWQYNSWULVXOVXMVYMVUDVXO
      VVIVYLVUDVXOUYAVYLVUDNVXNUCFVYLUYGVUDVXNVYLUYGNVXSVUDVXNUXFUXKUYGVXSVXPUX
      FVXQVXSVXRVXTYBXFVYAWQYAYCSSVYBYEUGVVPUYMUYLCDUYKTVXIVVMVVBVVPVXLUUMYOXAV
      YTWUQWUFLZVUDVYLVVIWVAUYAUXFVVIWVAUXKUXFVVINZWUCRGZWVAVVIUXFWVCUXFVVIWUBR
      GZWVCUXFARGZWVDAUUNZAUUOZWDVUOWUBUUPYBZUUQWUPWUFUGWUCRWUMWUCPZWUOWUECDFFW
      VIWUNWUDVUKWUMWUCWUAQYLUURUUTUUSWDSSVOUVAXMVYTUYSUYDUVBYPZFTUVCZWUGWUKLUY
      SVYTVUCYKUXFWVJUXKUYAVVIUXFAVLUVDHGZVLUYDGWVJUXFWVLAUVEUVFVLAUVGUYDVLUVHY
      QUVIWVKVYTUVKYKWUDBCDUYDFUVJXAVYTWUJVYQCFVYTWUIVYPDFVYTVVMNZWUHVYOVUKWVMV
      YTVWBNWUHVYOLZVYTVVMVWBVYLVVMVWBLZUYAVVIUXKWVOUXFUXKVVMVWBVWDYRXIYMVRVYMV
      VIVWBWVNUXFUYAVVIVWBNWVNUXKUXFVVIUYAVWBWVNUXFUYAVWBNZVVIWVNUXFWVPNUXFVWKV
      YINZNZVVIWVNUXFVWBUYAWVRUXFVWBUYANZWVQUXFWVSWVQUXFVWBVWKUYAVYIVXFWUTVQWCV
      RYFWVRVVIWUHVYOUXFVWKVYIVVIWUHNZVYOLUXFVWKVYIUVLZWVTVYOWWAWVTNVYNWUCWUBUV
      MOZVUOQWWAVVIWUHVYNWWBQJZWWAVVIWVCWUHWWCLUXFVWKVVIWVCLVYIVVIUXFWVCWVHWHUV
      NWWAWVCWUHWWCWUCBVUMUXLUYMAVWIVWPVWQUVOUVPUVQUVRWWAVVIWWBVUOPZWUHUXFVWKVV
      IWWDVYIWVBVUOYSGZWUBYSGZWUBYTYPZWWDVVIWWEUXFVUOUVSXIUXFWWFVVIUXFATGZYTAVF
      JZNWUBTGWWFUXFWWHWWIAUVTUXFAUWAGWWIAUWCAUWBWDVMAUWDWUBUWEYQVOUXFWWGVVIUXF
      WVEWVDWWGWVFWVGWUBUWFYQVOVUOWUBUWGXAUWHVHUWIYRWNUWJWQYAUWKYNXFWDXLXMUWMUW
      NUWLUXFUYAUXKUXMVYSXOZUXFUYAUXKWWJUXFUXHUYAUXKWWJUXQEVUMUXGUXLCDUXJUXIVXJ
      VVMVUMUWOYOUWPUWQYAXPYRSUWRUWSUWTUXCUXMUBUXIUXAUXDYRYCUBUXGUAUXIVXJUXEUXB
      $.
      $( [28-Feb-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d X a b d f m n r u v w x y z $.  $d M a b d f m n r u v w x y z $.
    $d Y a b d f m n r u v w x y z $.  $d N a b d f m n r u v w x y z $.
    rrntotbnd.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrntotbnd.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem1 $p |- ( ( ( ( N e. NN /\ x e. X ) /\ ( d e. RR+ /\ r e. RR+ )
          ) /\ ( y e. dom dom ( Rn ` N ) /\ ( x ( Rn ` N ) y ) < r ) ) ->
        ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
            ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\
            v = ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( ball ` ( Rn ` N ) ) d ) e.
        { a | E. f ( f : ( 1 ... N ) --> ( -u ( ( |_ ` ( r / ( d / ( sqr ` N )
            ) ) ) + 1 ) ... ( ( |_ ` ( r / ( d / ( sqr ` N ) ) ) ) + 1 ) ) /\
                a = ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
                                ( ( d / ( sqr ` N ) ) x. ( f ` m ) ) ) ) }
                                            ( ball ` ( Rn ` N ) ) d ) ) } ) $=
      ( wcel wa co cr cn cv crp crrn cfv cdm clt wbr c1 cfz csqr cdiv cfl caddc
      cneg wf cmin c2 wceq copab cmul cbl wex cab wral cabs cle cn0 simplr nnrp
      rpsqrcl syl rpdivcl sylan2 ancoms adantlr syl2anc cc0 rpre rpge0 flge0nn0
      nn0p1nn 3syl an31s ad2antrr wne cmap rrndm eleq2d reex oprex elmap syl6bb
      biimpa ffvelrn sylan adantllr eleq2i bitri adantll resubcl ad2ant2r rpne0
      cz sylanb redivcl syl3anc 2re 2ne0 rereccli readdcl sylancl flcl adantlrr
      zcn abscl recnd peano2zdi zre anasss peano2re absrdbnd absdiv absid eqtrd
      cc opreq2d cme adantr a1i wi eqbrtrd letrd wb sylib mpbid 3ad2ant1 eqeq2d
      eqid anbi2d recn rrnmet syl6reqr simpr metcl ad2antll ccom cres remetdval
      cxp remet remetba metsym biimprd imdistani rrndstprj1 ex anassrs eqbrtrrd
      an32s imp adantl ad2antlr ltle impr elrp lediv1 flwordi leadd1 nnre absle
      1re w3a syl2an biimp3a simp2 nnnegz mpbird ralrimiva fopab2 eqidd opabex2
      elfz feq1 fveq1 opabbidv opreq1d anbi12d cla4ev eqeq1 exbidv elab sylibr
      nnz ) IUAQZAUBZJQZRZNUBZUCQZLUBZUCQZRZRZBUBZIUDUEZUFUFZQZUWPUXEUXFSZUXAUG
      UHZRZRZUIIUJSZUXAUWSIUKUEZULSZULSZUMUEZUIUNSZUOZUXRUJSZEUBZUPZFUBZUXMQZDU
      BZUYCUWPUEZUXOUYCGUBZUXMQZCUBUYGUXEUEZUYGUWPUEZUQSZUXOULSZUIURULSZUNSZUMU
      EZUSRGCUTZUEZVASZUNSZUSZRZFDUTZUWSUXFVBUEZSZUYDUYEUYFUXOUYCUYAUEZVASZUNSZ
      USZRZFDUTZUWSVUCSZUSZRZEVCZVUDUYBMUBZVUKUSZRZEVCZMVDQUXLUXMUXTUYPUPZVUDVU
      DUSZVUNUXLUYOUXTQZGUXMVEVUSUXLVVAGUXMUXLUYHRZUXRUAQZUYOXDQZUYOVFUEZUXRVGU
      HZVVAUXDVVCUXKUYHUWOUXCVVCUWQUXCUWOVVCUWOUXBUWTVVCUWOUXBRUWTRZUXPUCQZUXQV
      HQZVVCVVGUXBUXOUCQZVVHUWOUXBUWTVIUWOUWTVVJUXBUWTUWOVVJUWOUWTUXNUCQZVVJUWO
      IUCQVVKIVJIVKVLUWSUXNVMVNZVOZVPUXAUXOVMZVQVVHUXPTQZVRUXPVGUHVVIUXPVSZUXPV
      TUXPWAVQUXQWBWCWDVOVPWEUXDUXHUYHVVDUXJUXDUXHRZUYHRZUYNTQZVVDVVRUYLTQZUYMT
      QVVSVVRUYKTQZUXOTQZUXOVRWFZVVTUWRUXHUYHVWAUXCUWRUXHRZUYHRZUYITQZUYJTQZVWA
      UWOUXHUYHVWFUWQUWOUXHRUXMTUXEUPZUYHVWFUWOUXHVWHUWOUXHUXETUXMWGSZQVWHUWOUX
      GVWIUXEIWHZWITUXMUXEWJUIIUJWKZWLWMWNUXMTUYGUXEWOWPWQZUWRUYHVWGUXHUWQUYHVW
      GUWOUWQUXMTUWPUPZUYHVWGUWQUWPVWIQVWMJVWIUWPOWRTUXMUWPWJVWKWLWSUXMTUYGUWPW
      OXEWTVPZUYIUYJXAVQWQZUXDVWBUXHUYHUWOUWTVWBUWQUXBUWTUWOVWBUWTUWORZVVJVWBVV
      LUXOVSVLVOZXBWEUXDVWCUXHUYHUWOUWTVWCUWQUXBUWTUWOVWCVWPVVJVWCVVLUXOXCVLVOX
      BZWEUYKUXOXFXGZURXHXIXJUYLUYMXKXLZUYNXMZVLXNVVBVVEUYLVFUEZUMUEZUIUNSZUXRV
      VBUYOYFQZVVETQVVBVVSVVDVXEUXDUXHUYHVVSUXJVWTXNVXAUYOXOWCUYOXPVLVVBVXBTQZV
      XDXDQVXDTQUXDUXHUYHVXFUXJVVRUYLYFQVXFVVRUYLVWSXQUYLXPVLXNZVXFVXCVXBXMZXRV
      XDXSWCVVBUXQTQZUXRTQZUXDVXIUXKUYHUWOUXCVXIUWQUWOUXCRVVOUXQXDQVXIUWOUWTUXB
      VVOUXBUWOUWTRZVVOUXBVXKRVVHVVOVXKUXBVVJVVHVVMVVNVNVVPVLVOXTZUXPXMUXQXSWCV
      PWEZUXQYAVLVVBVVTVVEVXDVGUHUXDUXHUYHVVTUXJVWSXNUYLYBVLVVBVXCUXQVGUHZVXDUX
      RVGUHZVVBVXFVVOVXBUXPVGUHVXNVXGUXDVVOUXKUYHUWOUXCVVOUWQVXLVPWEVVBVXBUYKVF
      UEZUXOULSZUXPVGVVBVXBVXPUXOVFUEZULSZVXQVVBUYKYFQZUXOYFQZVWCVXBVXSUSVVBUYK
      UXDUXHUYHVWAUXJVWOXNZXQUXDVYAUXKUYHUWOUWTVYAUWQUXBVXKUXOVWQXQXBWEUXDVWCUX
      KUYHVWRWEUYKUXOYCXGVVBVXRUXOVXPULUXDVXRUXOUSZUXKUYHUWOUWTVYCUWQUXBVXKVWBV
      RUXOVGUHZVYCVWQVXKVVJVYDVVMUXOVTVLUXOYDVQXBWEYGYEVVBVXPUXAVGUHZVXQUXPVGUH
      ZVVBVXPUXIUXAVVBVWAVXTVXPTQZVYBUYKUUAUYKXPWCZUXLUXITQZUYHUWRUXHVYIUXCUXJV
      WDUXFYHQZUWPUXGQZUXHVYIUWOVYJUWQUXHIUUBWEUWRVYKUXHUWOUWQVYKUWOJUXGUWPUWOU
      XGVWIJVWJOUUCZWIWNYIUWRUXHUUDUWPUXEUXFUXGUXGYSUUEXGZXBYIUXDUXATQZUXKUYHUX
      BVYNUWRUWTUXAVSZUUFWEZUWRUXKUYHVXPUXIVGUHZUXCUWRUXHUYHVYQUXJVWEUYIUYJVFUQ
      UUGTTUUJUUHZSZVXPUXIVGVWEVWFVWGVYSVXPUSVWLVWNUYIUYJVYRVYRYSZUUIVQVWEVYSUY
      JUYIVYRSZUXIVGVWEVYRYHQZVWFVWGVYSWUAUSWUBVWEVYRVYTUUKYJVWLVWNUYIUYJVYRTVY
      RVYTUULUUMXGVWDUWRUXEJQZRZUYHWUAUXIVGUHZUWRUXHWUCUWOUXHWUCYKUWQUWOWUCUXHU
      WOJUXGUXEVYLWIUUNYIUUOWUDUYHWUEUWOUWQWUCUYHWUEYKUWOUWQWUCRZRUYHWUEUWOUYHW
      UFWUEUYGUWPUXEVYRIJVYTOUUPUUTUUQUURUVAWPYLUUSXNWQUXLUXIUXAVGUHZUYHUXDUXHU
      XJWUGVVQVYIVYNUXJWUGYKUWRUXHVYIUXCVYMVPUXCVYNUWRUXHUXBVYNUWTVYOUVBUVCUXIU
      XAUVDVQUVEYIYMVVBVYGVYNVWBVRUXOUGUHRZVYEVYFYNVYHVYPVVBVVJWUHUXDVVJUXKUYHU
      WOUWTVVJUWQUXBVVMXBWEUXOUVFYOVXPUXAUXOUVGXGYPYLVXBUXPUVHXGVVBVXCTQZVXIUIT
      QZVXNVXOYNVVBVXFVXCXDQWUIVXGVXHVXCXSWCVXMWUJVVBUVLYJVXCUXQUIUVIXGYPYMVVCV
      VDVVFUVMZVVAUXSUYOVGUHUYOUXRVGUHRZVVCVVDVVFWULVVDVVCVVFWULYNZVVDUYOTQVXJW
      UMVVCUYOXSUXRUVJUYOUXRUVKUVNVOUVOWUKVVDUXSXDQZUXRXDQZVVAWULYNVVCVVDVVFUVP
      VVCVVDWUNVVFUXRUVQYQVVCVVDWUOVVFUXRUWNYQUYOUXSUXRUWCXGUVRXGUVSGCUXMUXTUYO
      UYPUYPYSUVTYOUXLVUDUWAVUMVUSVUTREUYPGCUXMUYOVWKUWBUYAUYPUSZUYBVUSVULVUTUX
      MUXTUYAUYPUWDWUPVUKVUDVUDWUPVUJVUBUWSVUCWUPVUIVUAFDWUPVUHUYTUYDWUPVUGUYSU
      YEWUPVUFUYRUYFUNWUPVUEUYQUXOVAUYCUYAUYPUWEYGYGYRYTUWFUWGYRUWHUWIVQVURVUNM
      VUDVUBUWSVUCWKVUOVUDUSZVUQVUMEWUQVUPVULUYBVUOVUDVUKUWJYTUWKUWLUWM $.
      $( [2-Sep-2009] $)

    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem2 $p |- ( ( ( N e. NN /\ d e. RR+ ) /\
          ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) ) ->
                  ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
        ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\ v =
                ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                      ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( Rn ` N ) y ) < d ) $=
      ( wcel wa cr co cfv wceq cmul va cn cv crp c1 cfz cmap csqr cdiv c2 caddc
      cmin cfl copab crrn clt cabs ccom cxp cres wbr wral simpll wf oprex elmap
      reex ffvelrn sylanb adantll adantlrr nnrp rpsqrcl syl rpdivcl sylan2 rpre
      ancoms ad2antrr cc0 wne adantlr resubcl syl2anc rpne0 redivcl syl3anc 2re
      cz 2ne0 rereccli readdcl sylancl flcl zre 3syl ralrimiva eqid sylib sylan
      fopab2 remulcl sylibr simprr adantr fveq2 opreq2d opreq12d fvopab4 adantl
      opreq1d fveq2d fvex jca simprl remetdval cc recnd subcl abscl a1i 1re cle
      rddif halflt1 lelttrd rpgt0 ltmul2 syl112anc mpbid recn ad2antrl ad2antll
      wb addcom subsub3 rpcn divcan2 eqcomd eqbrtrd subdi eqtr4d 3eqtr2d absmul
      rpge0 absid 3eqtrd mulid1 3brtr4d syl2an anassrs syl32anc divcan1 breqtrd
      rrndstprj2 ) HUBNZKUCZUDNZOZAUCZPUEHUFQZUGQZNZBUCZUVBNZOZOZEUCZUVANZDUCUV
      HUUTRZUUQHUHRZUIQZUVHFUCZUVANZCUCUVMUVDRZUVMUUTRZULQZUVLUIQZUEUJUIQZUKQZU
      MRZSOFCUNZRZTQZUKQZSOEDUNZUVDHUORQZUVLUVKTQZUUQUPUVGUUPUWFUVBNZUVEUVLUDNZ
      UAUCZUWFRZUWKUVDRZUQULURPPUSUTZQZUVLUPVAZUAUVAVBUWGUWHUPVAUUPUURUVFVCUVGU
      VAPUWFVDZUWIUVGUWEPNZEUVAVBUWQUVGUWREUVAUVGUVIOZUVJPNZUWDPNZUWRUUSUVCUVIU
      WTUVEUVCUVIUWTUUSUVCUVAPUUTVDZUVIUWTPUVAUUTVGUEHUFVEZVFZUVAPUVHUUTVHVIVJV
      KUWSUVLPNZUWCPNZUXAUUSUXEUVFUVIUURUUPUXEUURUUPOZUWJUXEUUPUURUVKUDNZUWJUUP
      HUDNZUXHHVLZHVMZVNUUQUVKVOVPZUVLVQZVNVRZVSUVGUVAPUWBVDZUVIUXFUVGUWAPNZFUV
      AVBUXOUVGUXPFUVAUVGUVNOZUVTPNZUWAWINUXPUXQUVRPNZUVSPNZUXRUXQUVQPNZUXEUVLV
      TWAZUXSUVFUVNUYAUUSUVFUVNOUVOPNZUVPPNZUYAUVEUVNUYCUVCUVEUVAPUVDVDZUVNUYCP
      UVAUVDVGUXCVFZUVAPUVMUVDVHVIVJUVCUVNUYDUVEUVCUXBUVNUYDUXDUVAPUVMUUTVHVIWB
      UVOUVPWCWDVJUUSUXEUVFUVNUXNVSUUSUYBUVFUVNUURUUPUYBUXGUWJUYBUXLUVLWEZVNVRV
      SUVQUVLWFWGUJWHWJWKZUVRUVSWLWMUVTWNUWAWOWPWQFCUVAPUWAUWBUWBWRZXAWSUVAPUVH
      UWBVHWTUVLUWCXBWDUVJUWDWLWDWQEDUVAPUWEUWFUWFWRZXAWSPUVAUWFVGUXCVFXCUUSUVC
      UVEXDUUSUWJUVFUURUUPUWJUXLVRZXEUVGUWPUAUVAUVGUWKUVANZOZUWOUWKUUTRZUVLUWKU
      WBRZTQZUKQZUWMUWNQZUVLUPUYLUWOUYRSUVGUYLUWLUYQUWMUWNEDUWKUWEUYQUVAUWFUVHU
      WKSZUVJUYNUWDUYPUKUVHUWKUUTXFUYSUWCUYOUVLTUVHUWKUWBXFXGXHUYJUYNUYPUKVEXIX
      KXJUYMUYRUYNUVLUWMUYNULQZUVLUIQZUVSUKQZUMRZTQZUKQZUWMUWNQZUVLUPUYLUYRVUFS
      UVGUYLUYQVUEUWMUWNUYLUYPVUDUYNUKUYLUYOVUCUVLTFCUWKUWAVUCUVAUWBUVMUWKSZUVT
      VUBUMVUGUVRVUAUVSUKVUGUVQUYTUVLUIVUGUVOUWMUVPUYNULUVMUWKUVDXFUVMUWKUUTXFX
      HXKXKXLUYIVUBUMXMXIXGXGXKXJUUSUVFUYLVUFUVLUPVAZUUSUWJUYNPNZUWMPNZOZVUHUVF
      UYLOZUYKVULVUIVUJUVCUYLVUIUVEUVCUXBUYLVUIUXDUVAPUWKUUTVHVIWBUVEUYLVUJUVCU
      VEUYEUYLVUJUYFUVAPUWKUVDVHVIVJXNUWJVUKOZVUFVUEUWMULQZUQRZUVLUPVUMVUEPNZVU
      JVUFVUOSVUMVUIVUDPNZVUPUWJVUIVUJXOVUMUXEVUCPNZVUQUWJUXEVUKUXMXEZVUMVUBPNZ
      VUCWINVURVUMVUAPNZUXTVUTVUMUYTPNZUXEUYBVVAVUKVVBUWJVUJVUIVVBUWMUYNWCVRXJZ
      VUSUWJUYBVUKUYGXEZUYTUVLWFWGZUYHVUAUVSWLWMVUBWNVUCWOWPZUVLVUCXBWDZUYNVUDW
      LWDUWJVUIVUJXDVUEUWMUWNUWNWRZXPWDVUMUVLVUCVUAULQZUQRZTQZUVLUETQZVUOUVLUPV
      UMVVJUEUPVAZVVKVVLUPVAZVUMVVJUVSUEVUMVVIXQNZVVJPNZVUMVUCXQNZVUAXQNZVVOVUM
      VUCVVFXRZVUMVUAVVEXRZVUCVUAXSWDZVVIXTVNZUXTVUMUYHYAUEPNZVUMYBYAZVUMVVAVVJ
      UVSYCVAVVEVUAYDVNUVSUEUPVAVUMYEYAYFVUMVVPVWCUXEVTUVLUPVAZVVMVVNYNVWBVWDVU
      SUWJVWEVUKUVLYGXEVVJUEUVLYHYIYJVUMVUOUVLVVITQZUQRZUVLUQRZVVJTQZVVKVUMVUNV
      WFUQVUMVUNVUDUYNUKQZUWMULQZVUDUYTULQZVWFVUMVUEVWJUWMULVUMUYNXQNZVUDXQNZVU
      EVWJSVUIVWMUWJVUJUYNYKYLZVUMVUDVVGXRZUYNVUDYOWDXKVUMVWNUWMXQNZVWMVWLVWKSV
      WPVUJVWQUWJVUIUWMYKYMVWOVUDUWMUYNYPWGVUMVWLVUDUVLVUATQZULQZVWFVUMUYTVWRVU
      DULVUMVWRUYTVUMUYTXQNUVLXQNZUYBVWRUYTSVUMUYTVVCXRUWJVWTVUKUVLYQZXEZVVDUYT
      UVLYRWGYSXGVUMVWTVVQVVRVWFVWSSVXBVVSVVTUVLVUCVUAUUAWGUUBUUCXLVUMVWTVVOVWG
      VWISVXBVWAUVLVVIUUDWDVUMVWHUVLVVJTVUMUXEVTUVLYCVAZVWHUVLSVUSUWJVXCVUKUVLU
      UEXEUVLUUFWDXKUUGVUMVVLUVLUWJVVLUVLSZVUKUWJVWTVXDVXAUVLUUHVNXEYSUUIYTUUJU
      UKYTYTWQUVLUAUWFUVDUWNHUVBVVHUVBWRUUOUULUUSUWHUUQSZUVFUUSUUQXQNZUVKXQNZUV
      KVTWAZVXEUURVXFUUPUUQYQXJUUPVXGUURUUPUXIUXHVXGUXJUXKUVKYQWPXEUUPVXHUURUUP
      UXIUXHVXHUXJUXKUVKWEWPXEUUQUVKUUMWGXEUUN $.
      $( [2-Sep-2009] $)

    $( A set in Euclidean space is totally bounded iff its is bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrntotbnd $p |- ( ( N e. NN /\ Y C_ X ) ->
                                      ( M e. TotBnd <-> M e. Bnd ) ) $=
      ( vx vb vf vm wcel wss wa cv cfv co wceq cfn cr c0 vr vv vy vd va ctotbnd
      vu vz vn cbnd cdm eqid totbndbnd cbl crp wrex wral cuni crrn cxp cres a1i
      cn dmeqd cme rrnmet adantr cfz cmap rrndm syl6eqr sseq2d biimpar metssba2
      c1 syl2anc eqtr4d raleqdv wi 0fin id eqsstrd adantl unieq anbi12d rcla4ev
      0ss raleq ralrimiva wne cdiv cfl caddc wf cmul wex cab anim2i anassrs clt
      copab wbr wb ad2antrr eleq2d simprr syl3anc c2 biimpi oprex elmap ffvelrn
      reex sylanb adantll adantlrr sylan2 ancoms cz adantlr readdcl 3syl fopab2
      sylib sylan remulcl syl6bb mpbird ad2ant2rl an32s adantrr ex sylbid bitri
      syl sstri fzfi mp2an cvv ax-mp ral0 jctir sylancr a1d wn r19.2z csqr cneg
      df-ne ssel2 anim1i anasss biimpa elbl cmin rrntotbndlem1 biimpd imdistani
      syl6reqr eleq2i rrntotbndlem2 nnrp rpsqrcl rpdivcl rpre cc0 resubcl rpne0
      simplr redivcl 2re 2ne0 rereccli sylancl flcl zre elbl2 syl22anc expr imp
      sylanl1 eleq2 eluni2 sylibr ssrdv eqeq1d jca blssp inss1 sseq1 syl5ibrcom
      an4s cin sylbird sstr expcom sylsyld vex eqeq1 anbi2d exbidv fzssuz uzssz
      elab cuz zssre syl2an opreq1 eqeq2d exlimdv syl5bi ralrimiv jctird df-rex
      fss mpan2 anbi1i exbii abbii xpfi pwfi mpbi mapsspw ssfi abrexfi eqeltrri
      cpw mpan syl6 ralrimdva rexlimdvv syl5 sylan2br pm2.61dan metres syl5eqel
      expdimp isbnd2 sstotbnd 3imtr4d impbid2 ) BVCKZDCLZMZAUFKZAUJKZAAUKZUKZVU
      HULZUMVUDVUHGNZUANZAUNOPZQZUAUOUPZGVUHUQZDUBNZURZLZHNZUCNZUDNZBUSOZUNOZPZ
      QZUCVVBUKUKZUPZHVUPUQZMZUBRUPZUDUOUQZVUFVUEVUDVUOVUNGDUQZVVKVUDVUNGVUHDVU
      DVUHVVBDDUTVAZUKZUKZDVUDVUGVVNVUDAVVMAVVMQVUDFVBVDVDZVUDVVBVEKZDVVFLZDVVO
      QVUBVVQVUCBVFZVGZVUBVVRVUCVUBVVFCDVUBVVFSVOBVHPZVIPZCBVJZEVKVLVMZVVBVVFDV
      VFULZVNVPZVQVRVUDDTQZVVLVVKVSZVUDVWGMZVVKVVLVWIVVJUDUOVWIVVJVVAUOKZVWITRK
      DTURZLZVVGHTUQZMZVVJVTVWIVWLVWMVWGVWLVUDVWGDTVWKVWGWATVWKLVWGVWKWGVBWBWCV
      VGHUUAUUBVVIVWNUBTRVUPTQZVURVWLVVHVWMVWOVUQVWKDVUPTWDVLVVGHVUPTWHWEWFUUCV
      GWIUUDVWGUUEVUDDTWJZVWHDTUUIVUDVWPVVLVVKVWPVVLMVUNGDUPVUDVVKVUNGDUUFVUDVU
      MVVKGUADUOVUDVUJDKZVUKUOKZMZVUMVVKVSVUDVWSMZVUMVVJUDUOVWTVWJMZVUMDVWAVUKV
      VABUUGOZWKPZWKPWLOVOWMPZUUHZVXDVHPZINZWNZUENZJNZVWAKZUGNZVXJVUJOZVXCVXJVX
      GOZWOPZWMPZQMJUGXAZVVAVVCPZQZMZIWPZUEWQZURZLZVVGHVYBUQZMZVVJVXAVUMVYDVYEV
      XAVUJVUKVVCPZVYCLZVUMDVYGLZVYDVXAUCVYGVYCVWTVUBVUJCKZVWRMZMZVWJVUTVYGKZVU
      TVYCKZVSZVUBVUCVWSVYLVUCVWSMVYKVUBVUCVWQVWRVYKVUCVWQMZVYJVWRDCVUJUUJZUUKU
      ULWRWSVUBVWJVYKVYOVUBVYJVWJVWRVYOVUBVYJMZVWJVWRMZMZVYMVUTVVFKZVUJVUTVVBPV
      UKWTXBZMZVYNVYTVVQVUJVVFKZVWRVYMWUCXCVUBVVQVYJVYSVVSXDVYRWUDVYSVUBVYJWUDV
      UBCVVFVUJVUBVVFVWBCVWCEUUSXEUUMVGVYRVWJVWRXFVUTVVBVUJVUKVVFVWEUUNXGVYTWUC
      VYNVYTWUCMZVUTUHNZKZUHVYBUPZVYNWUEVXKVXLVXMVXCVXJUINZVWAKZVUPWUIVUTOZWUIV
      UJOZUUOPZVXCWKPZVOXHWKPZWMPZWLOZQMUIUBXAZOZWOPZWMPZQMJUGXAZVVAVVCPZVYBKVU
      TWVCKZWUHGUCUBUGIJUIABCDUAUEUDEFUUPVYTWUAWVDWUBVYRVWJWUAWVDVWRVYRVWJMZWUA
      MWVEVUTVWBKZMWVDWVEWUAWVFVUBWUAWVFVSVYJVWJVUBWUAWVFVUBVVFVWBVUTVWCXEZUUQX
      DUURVYRVUBVUJVWBKZMZVWJWVFWVDVYJWVHVUBVYJWVHCVWBVUJEUUTZXIWRWVIVWJMWVFWVD
      VUBVWJWVHWVFWVDVSVUBVWJMZWVHWVFWVDWVKWVHWVFMZMZWVDWVBVUTVVBPVVAWTXBZGUCUB
      UGJUIABCDUDEFUVAWVMVVQVWJWVBVVFKZWUAWVDWVNXCVUBVVQVWJWVLVVSXDVUBVWJWVLUVI
      WVMWVOVWASWVBWNZWVMWVASKZJVWAUQWVPWVMWVQJVWAWVMVXKMZVXMSKZWUTSKZWVQWVKWVH
      VXKWVSWVFWVHVXKWVSWVKWVHVWASVUJWNZVXKWVSSVWAVUJXMVOBVHXJZXKZVWASVXJVUJXLZ
      XNXOXPWVRVXCSKZWUSSKZWVTWVKWWEWVLVXKVWJVUBWWEVWJVUBMZVXCUOKZWWEVUBVWJVXBU
      OKZWWHVUBBUOKWWIBUVBBUVCYOVVAVXBUVDXQZVXCUVEYOXRZXDWVMVWASWURWNZVXKWWFWVM
      WUQSKZUIVWAUQWWLWVMWWMUIVWAWVMWUJMZWUPSKZWUQXSKWWMWWNWUNSKZWUOSKWWOWWNWUM
      SKZWWEVXCUVFWJZWWPWVLWUJWWQWVKWVLWUJMWUKSKZWULSKZWWQWVFWUJWWSWVHWVFVWASVU
      TWNWUJWWSSVWAVUTXMWWBXKVWASWUIVUTXLXNXOWVHWUJWWTWVFWVHWWAWUJWWTWWCVWASWUI
      VUJXLXNXTWUKWULUVGVPXOWVKWWEWVLWUJWWKXDWVKWWRWVLWUJVWJVUBWWRWWGWWHWWRWWJV
      XCUVHYOXRXDWUMVXCUVJXGXHUVKUVLUVMWUNWUOYAUVNWUPUVOWUQUVPYBWIUIUBVWASWUQWU
      RWURULYCYDVWASVXJWURXLYEVXCWUSYFVPVXMWUTYAVPWIJUGVWASWVAWVBWVBULYCYDWVMWV
      OWVBVWBKWVPWVMVVFVWBWVBVUBVVFVWBQVWJWVLVWCXDXESVWAWVBXMWWBXKYGYHVUBWVFWUA
      VWJWVHVUBWUAWVFWVGVMYIVUTVVBWVBVVAVVFVWEUVQUVRYHUVSYJUVTUWAYOXPYKWUGWVDUH
      WVCVYBWUFWVCVUTUWBWFVPUHVUTVYBUWCUWDYLYMUWLYJYEUWEVWTVUMVYIVSVWJVWTVUMDVU
      LQZVYIVWTDVUHVULVUDDVUHQVWSVUDDVVOVUHVWFVVPVQVGUWFVWTVYIWXAVULVYGLZVUDVVQ
      VVRMZVWSWXBVUDVVQVVRVVTVWDUWGWXCVWSMZVULVYGDUWMZVYGVUKDVVBAVVFVUJVWEFUWHW
      XEVYGLWXDVYGDUWIVBWBYEDVULVYGUWJUWKUWNVGVYIVYHVYDDVYGVYCUWOUWPUWQVXAVVGHV
      YBVUSVYBKVXHVUSVXRQZMZIWPZVXAVVGVYAWXHUEVUSHUWRVXIVUSQZVXTWXGIWXIVXSWXFVX
      HVXIVUSVXRUWSUWTUXAUXDVXAWXGVVGIVXAWXGVVGVXAWXGMVXQVVFKZWXFVVGVXAVXHWXJWX
      FVXAVXHMZWXJVWASVXQWNZWXKVXPSKZJVWAUQWXLWXKWXMJVWAVXAVXHVXKWXMVXAVXHVXKMZ
      MWVSVXOSKZWXMVWTVXKWVSVWJVXHVUDVWQVXKWVSVWRVUDVWQMWWAVXKWVSVUBVUCVWQWWAVY
      PVUBVYJWWAVYQVYJWWAVUBVYJWWAVYJWVHWWAWVJWWCYNXIWCXQWSWWDYEXPYIVXAWWEVXNSK
      ZWXOWXNVUDVWJWWEVWSVUBVWJWWEVUCWWKXTXTVXHVWASVXGWNZVXKWXPVXHVXFSLWXQVXFXS
      SVXFVXEUXEOXSVXEVXDUXBVXEUXCYPUXFYPVWAVXFSVXGUXOUXPVWASVXJVXGXLYEVXCVXNYF
      UXGVXMVXOYAVPWSWIJUGVWASVXPVXQVXQULYCYDVWTWXJWXLXCZVWJVXHVUBWXRVUCVWSVUBW
      XJVXQVWBKWXLVUBVVFVWBVXQVWCXESVWAVXQXMWWBXKYGXDXDYHYKVXAVXHWXFXFVVEWXFUCV
      XQVVFVUTVXQQVVDVXRVUSVUTVXQVVAVVCUXHUXIWFVPYLUXJUXKUXLUXMVYBRKVYFVVJVXSIV
      XFVWAVIPZUPZUEWQZVYBRWXTVYAUEWXTVXGWXSKZVXSMZIWPVYAVXSIWXSUXNWYCVXTIWYBVX
      HVXSVXFVWAVXGVXEVXDVHXJWWBXKUXQUXRYNUXSWXSRKZWYARKVWAVXFUTZUYGZRKZWXSWYFL
      ZWYDWYERKZWYGVWARKVXFRKWYIVOBYQVXEVXDYQVWAVXFUXTYRWYEUYAUYBVWAYSKWYHWWBVX
      FVWAYSUYCYTWYFWXSUYDYRIUEWXSVXRUYEYTUYFVVIVYFUBVYBRVUPVYBQZVURVYDVVHVYEWY
      JVUQVYCDVUPVYBWDVLVVGHVUPVYBWHWEWFUYHUYIUYJYLUYKUYLUYQUYMUYNYMVUDVVQAVEKV
      UFVUOXCVVTVVQAVVMVEFVVBDUYOUYPGAVUHUAVUIUYRYBVUDVVQVVRVUEVVKXCVVTVWDUCUBV
      VBAVVFDHUDVWEFUYSVPUYTVUA $.
      $( [28-Feb-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    rrnheibor.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrnheibor.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    rrnheibor.3 $e |- T = ( MetOpen ` M ) $.
    rrnheibor.4 $e |- U = ( MetOpen ` ( Rn ` N ) ) $.
    $( Heine-Borel theorem for Euclidean space.  A subset of Euclidean space is
       compact iff it is closed and bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnheibor $p |- ( ( N e. NN /\ Y C_ X ) ->
                      ( T e. Comp <-> ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( wcel wss wa cms cfv cme adantr cdm eqid co ccomp ctotbnd ccld cbnd crrn
      rrnmet cxp cres metres syl5eqel syl biantrurd heibor syl6bb eleq1i rrncms
      cn wb cr c1 cfz cmap rrndm syl6reqr sseq2d biimpa cmsss syl2anc rrntotbnd
      syl5bb anbi12d bitrd ) DUQKZFELZMZAUAKZCNKZCUBKZMZFBUCOKZCUDKZMVOVPCPKZVP
      MVSVOWBVPVMWBVNVMDUEOZPKZWBDUFWDCWCFFUGUHZPHWCFUIUJUKQULCACRRZIWFSUMUNVOV
      QVTVRWAVQWENKZVOVTCWENHUOVOWCNKZFWCRRZLZWGVTURVMWHVNDUPQVMVNWJVMEWIFVMWIU
      SUTDVATVBTEDVCGVDVEVFWCBWIFWISJVGVHVJCDEFGHVIVKVL $.
      $( [2-Sep-2009] $)
  $}

