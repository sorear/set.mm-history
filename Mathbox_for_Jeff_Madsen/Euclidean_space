$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Euclidean space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The real numbers are a complete metric space. $)
    recms $p |- ( ( abs o. - ) |` ( RR X. RR ) ) e. CMet $=
      ( vy vx cabs cmin ccom cr cxp cres cms wcel copn cfv ccld cc cdif wss
      cc0 cv clt wbr cbl co wa wrex wral cme wb eqid cnmet cnmetba isopn4
      ax-mp difss cim eldifi imcl recn abscl syl 3syl cle wne 0re ltlen
      sylancr absge0 wn eldifn wceq reim0b necon3bbid mpbid abs00 necon3bid
      mpbird mpbir2and a1i blssm syl22anc sseld elbl c2 cexp cre caddc subcl
      syl2an recl sqge0 adantr resqcl addge02 syl11anc absresq absvalsq2 imsub
      reim0 adantl opreq2d subid1 3eqtrd opreq1d eqtrd 3brtr4d le2sq cnmetdval
      breq2d metcl syl111anc lenlt bitr3d ex con2d adantld sylbid jcad eldif
      syl6ibr ssrdv breq2 opreq2 sseq1d anbi12d rcla4ev syl12anc rgen mpbir2an
      ctop opntop axresscn cuni uniopn2 eqcomd iscld2 mp2an mpbir cncms cmsss
      ) CDEZFFGHIJZFYSKLZMLJZUUBNFOZUUAJZUUDUUCNPZQARZSTZBRZUUFYSUALZUBZUUCPZUC
      ZAFUDZBUUCUEZYSUFJZUUDUUEUUNUCUGYSYSUHZUIZBAUUCYSUUANYSUUPUJZUUAUHZUKULNF
      UMUUMBUUCUUHUUCJZUUHUNLZCLZFJZQUVBSTZUUHUVBUUIUBZUUCPZUUMUUTUUHNJZUVAFJZU
      VCUUHNFUOZUUHUPZUVHUVANJZUVCUVAUQZUVAURUSUTZUUTUVDQUVBVATZUVBQVBZUUTQFJUV
      CUVDUVNUVOUCUGVCUVMQUVBVDVEUUTUVKUVNUUTUVGUVHUVKUVIUVJUVLUTZUVAVFUSZUUTUV
      OUVAQVBZUUTUUHFJZVGUVRUUHNFVHUUTUVSUVAQUUTUVGUVSUVAQVIZUGUVIUUHVJUSVKVLUU
      TUVBQUVAQUUTUVKUVBQVIUVTUGUVPUVAVMUSVNVOVPZUUTAUVEUUCUUTUUFUVEJZUUFNJZUUF
      FJZVGZUCUUFUUCJUUTUWBUWCUWEUUTUVENUUFUUTUUOUVGUVCUVDUVENPUUOUUTUUQVQZUVIU
      VMUWAYSUUHUVBNUURVRVSVTUUTUWBUWCUUHUUFYSUBZUVBSTZUCZUWEUUTUUOUVGUVCUVDUWB
      UWIUGUWFUVIUVMUWAUUFYSUUHUVBNUURWAVSUUTUWHUWEUWCUUTUWDUWHUUTUWDUWHVGZUUTU
      WDUCZUVBUUHUUFDUBZCLZVATZUWJUWKUWNUVBWBWCUBZUWMWBWCUBZVATZUWKUVAWBWCUBZUW
      LWDLZWBWCUBZUWRWEUBZUWOUWPVAUWKQUWTVATZUWRUXAVATZUWKUWLNJZUWSFJZUXBUUTUVG
      UWCUXDUWDUVIUUFUQZUUHUUFWFWGZUWLWHZUWSWIUTUWKUWRFJZUWTFJZUXBUXCUGUWKUVHUX
      IUUTUVHUWDUUTUVGUVHUVIUVJUSWJZUVAWKUSUWKUXDUXEUXJUXGUXHUWSWKUTUWRUWTWLWMV
      LUWKUVHUWOUWRVIUXKUVAWNUSUWKUWPUWTUWLUNLZWBWCUBZWEUBZUXAUWKUXDUWPUXNVIUXG
      UWLWOUSUWKUXMUWRUWTWEUWKUXLUVAWBWCUWKUXLUVAUUFUNLZDUBZUVAQDUBZUVAUUTUVGUW
      CUXLUXPVIUWDUVIUXFUUHUUFWPWGUWKUXOQUVADUWDUXOQVIUUTUUFWQWRWSUWKUVKUXQUVAV
      IUUTUVKUWDUVPWJUVAWTUSXAXBWSXCXDUWKUVCUVNUWMFJZQUWMVATZUWNUWQUGUUTUVCUWDU
      VMWJZUUTUVNUWDUVQWJUWKUXDUXRUXGUWLURUSUWKUXDUXSUXGUWLVFUSUVBUWMXEVSVOUWKU
      VBUWGVATZUWNUWJUWKUWGUWMUVBVAUUTUVGUWCUWGUWMVIUWDUVIUXFUUHUUFYSUUPXFWGXGU
      WKUVCUWGFJZUYAUWJUGUXTUWKUUOUVGUWCUYBUUOUWKUUQVQUUTUVGUWDUVIWJUWDUWCUUTUX
      FWRUUHUUFYSNUURXHXIUVBUWGXJWMXKVLXLXMXNXOXPUUFNFXQXRXSUULUVDUVFUCAUVBFUUF
      UVBVIZUUGUVDUUKUVFUUFUVBQSXTUYCUUJUVEUUCUUFUVBUUHUUIYAYBYCYDYEYFYGUUAYHJZ
      FNPZUUBUUDUGUUOUYDUUQYSUUAUUSYIULYJFUUANUUONUUAYKZVIUUQUUOUYFNYSUUANUURUU
      SYLYMULYNYOYPYSIJUYEYTUUBUGYSUUPYQYJYSUUANFUURUUSYRYOYP $.
      $( [2-Sep-2009] $)
  $}

  $c Rn $.
  $( Extend class notation with the n-dimensional Euclidean space. $)
  crrn $a class Rn $.

  ${
    $d n r x y d k $.
    $( Define n-dimensional Euclidean space as a metric space with the standard
       Euclidean norm given by the quadratic mean. $)
    df-rrn $a |- Rn = { <. n , r >. | ( n e. NN /\ r = { <. <. x , y >. , d >.
          | ( ( x e. ( RR ^m ( 1 ... n ) ) /\ y e. ( RR ^m ( 1 ... n ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... n )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) } $.
  $}

  ${
    $d N n r x y d k $.
    $( The n-dimensional Euclidean space. $)
    rrnval $p |- ( N e. NN -> ( Rn ` N ) = { <. <. x , y >. , d >. |
            ( ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... N )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) $=
      ( vn vr cv cr c1 cfz co cmap wcel wa cfv cmin c2 cexp csu csqr wceq
      copab2 cn crrn opreq2 opreq2d eleq2d anbi12d sumeq1d fveq2d eqeq2d
      oprabbidv df-rrn oprex eqid oprabex2 fvopab4 ) FGDAHZIJFHZKLZMLZNZBHZVBNZ
      OZEHZVACHZUSPVHVDPQLRSLZCTZUAPZUBZOZABEUCUSIJDKLZMLZNZVDVONZOZVGVNVICTZUA
      PZUBZOZABEUCZUDUEUTDUBZVMWBABEWDVFVRVLWAWDVCVPVEVQWDVBVOUSWDVAVNIMUTDJKUF
      ZUGZUHWDVBVOVDWFUHUIWDVKVTVGWDVJVSUAWDVAVNVICWEUJUKULUIUMABCFGEUNABEVOVOV
      TWCIVNMUOZWGWCUPUQUR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.  $d X x y d k $.  $d Y x y d k $.
    $( The value of the Euclidean metric. $)
    rrnmval $p |- ( ( N e. NN /\ X e. ( RR ^m ( 1 ... N ) )
                  /\ Y e. ( RR ^m ( 1 ... N ) ) ) -> ( X ( Rn ` N ) Y ) =
        ( sqr ` sum_ k e. ( 1 ... N ) ( ( ( X ` k ) - ( Y ` k ) ) ^ 2 ) ) ) $=
      ( vx vy vd cn wcel cr c1 cfz co cmap crrn cfv cv cmin c2 cexp csu csqr
      wceq wa copab2 rrnval opreqd fvex fveq1 opreq1d sumeq2sdv fveq2d opreq2d
      eqid oprabval2 sylan9eq 3impb ) BHIZCJKBLMZNMZIZDUTIZCDBOPZMZUSAQZCPZVEDP
      ZRMZSTMZAUAZUBPZUCURVAVBUDVDCDEQZUTIFQZUTIUDGQUSVEVLPZVEVMPZRMZSTMZAUAZUB
      PZUCUDEFGUEZMVKURVCVTCDEFABGUFUGEFGCDUTUTVSVKVTUSVFVORMZSTMZAUAZUBPVJUBUH
      VLCUCZVRWCUBWDUSVQWBAWDVPWASTWDVNVFVORVEVLCUIUJUJUKULVMDUCZWCVJUBWEUSWBVI
      AWEWAVHSTWEVOVGVFRVEVMDUIUMUJUKULVTUNUOUPUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.
    $( The underlying set of Euclidean space. $)
    rrndm $p |- ( N e. NN -> dom dom ( Rn ` N ) = ( RR ^m ( 1 ... N ) ) ) $=
      ( vx vy vd vk cn wcel crrn cfv cdm cr c1 cfz co cmap cxp cv wa cmin c2
      cexp csu csqr wceq copab2 rrnval dmeqd fvex eqid dmoprab2 syl6eq c0 wne
      wn cc0 0re n0i ax-mp reex oprex map0 simplbi mto df-ne mpbir a1i dmxp
      syl eqtrd ) AFGZAHIZJZJKLAMNZONZVNPZJZVNVJVLVOVJVLBQZVNGCQZVNGRDQVMEQZVQI
      VSVRISNTUANEUBZUCIZUDRBCDUEZJVOVJVKWBBCEADUFUGBCDVNVNWAWBVTUCUHWBUIUJUKUG
      VJVNULUMZVPVNUDWCVJWCVNULUDZUNWDKULUDZUOKGWEUNUPKUOUQURWDWEVMULUMKVMUSLAM
      UTVAVBVCVNULVDVEVFVNVNVGVHVI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k z m n $.
    $( Euclidean space is a metric space. $)
    rrnmet $p |- ( N e. NN -> ( Rn ` N ) e. Met ) $=
      ( vx vy vz vd vk vm vn cn wcel crrn cfv cdm cxp cr wf cv co cc0 wceq wb
      caddc cle wbr wral wa cme c1 cfz cmap cmin c2 cexp csu csqr copab2 w3a
      cuz elnnuz biimpi 3ad2ant1 reex oprex elmap ffvelrn sylanb 3ad2antl2
      3ad2antl3 resubcl syl11anc resqcl syl r19.21aiva fsumrecl sqge0 jca
      fsumcmp0 sqrcl 3expib r19.21aivv eqid foprab2 sylib rrnval feq1d mpbird
      rrndm xpeq1 xpeq2 eqtrd feq2d eleq2d anbi12d rrnmval eqeq1d sqr00 fsum00
      cc recn sqeq0 3syl recnd subeq0 bitrd ralbidva wfn ffn sylbi eqidd
      biantrurd eqfnfv bitr4d syl2an 3adant1 3bitrd wi biimpd 3anbi123i copab
      simpl 3ad2antl1 fopab2 adantl trirn syl111anc fveq2 opreq12d fvopab4
      npncan adantll eqtr2d opreq1d sumeq2dv fveq2d cneg negsubdi2 subcl sqneg
      eqcomd 3eqtr3d 3brtr4d sylan2b 3adant3r3 3expb ancom2s 3adantr2 3adantr1
      3exp2 3imp syld r19.21aiv sylbid cvv fvex ismet ax-mp sylanbrc ) AIJZAKLZ
      MMZUUTNZOUUSPZBQZCQZUUSRZSTZUVCUVDTZUAZUVEDQZUVCUUSRZUVIUVDUUSRZUBRZUCUDZ
      DUUTUEZUFZCUUTUEBUUTUEZUUSUGJZUURUVBOUHAUIRZUJRZUVSNZOUUSPZUURUWAUVTOUVCU
      VSJZUVDUVSJZUFZEQUVRFQZUVCLZUWEUVDLZUKRZULUMRZFUNZUOLZTUFBCEUPZPZUURUWKOJ
      ZCUVSUEBUVSUEUWMUURUWNBCUVSUVSUURUWBUWCUWNUURUWBUWCUQZUWJOJZSUWJUCUDZUWNU
      WOAUHURLJZUWIOJZFUVRUEUWPUURUWBUWRUWCUURUWRAUSUTVAZUWOUWSFUVRUWOUWEUVRJZU
      FZUWHOJZUWSUXBUWFOJZUWGOJZUXCUWBUURUXAUXDUWCUWBUVROUVCPZUXAUXDOUVRUVCVBUH
      AUIVCZVDZUVROUWEUVCVEZVFVGZUWCUURUXAUXEUWBUWCUVROUVDPZUXAUXEOUVRUVDVBUXGV
      DZUVROUWEUVDVEZVFVHZUWFUWGVIVJZUWHVKVLZVMUWIFUHAVNVJZUWOUWRUWSSUWIUCUDZUF
      ZFUVRUEZUWQUWTUWOUXSFUVRUXBUWSUXRUXPUXBUXCUXRUXOUWHVOVLVPVMZUWIFUHAVQVJZU
      WJVRVJVSVTBCEUVSUVSUWKOUWLUWLWAWBWCUURUVTOUUSUWLBCFAEWDWEWFUURUVAUVTOUUSU
      URUVAUVSUUTNZUVTUURUUTUVSTZUVAUYCTAWGZUUTUVSUUTWHVLUURUYDUYCUVTTUYEUUTUVS
      UVSWIVLWJWKWFUURUVOBCUUTUUTUURUVCUUTJZUVDUUTJZUFUWDUVOUURUYFUWBUYGUWCUURU
      UTUVSUVCUYEWLUURUUTUVSUVDUYEWLWMUURUWBUWCUVOUWOUVHUVNUWOUVFUWKSTZUWJSTZUV
      GUWOUVEUWKSFAUVCUVDWNZWOUWOUWPUWQUYHUYIUAUXQUYBUWJWPVJUWOUYIUWISTZFUVRUEZ
      UWFUWGTZFUVRUEZUVGUWOUWRUXTUYIUYLUAUWTUYAUWIFUHAWQVJUWOUYKUYMFUVRUXBUYKUW
      HSTZUYMUXBUXCUWHWRJUYKUYOUAUXOUWHWSUWHWTXAUXBUWFWRJZUWGWRJZUYOUYMUAUXBUWF
      UXJXBUXBUWGUXNXBUWFUWGXCVJXDXEUWBUWCUYNUVGUAZUURUWBUVCUVRXFZUVDUVRXFZUYRU
      WCUWBUXFUYSUXHUVROUVCXGXHUWCUXKUYTUXLUVROUVDXGXHUYSUYTUFZUYNUVRUVRTZUYNUF
      UVGVUAVUBUYNVUAUVRXIXJFUVRUVRUVCUVDXKXLXMXNXOXOUWOUVMDUUTUWOUVIUUTJZUVIUV
      SJZUVMUURUWBVUCVUDXPUWCUURVUCVUDUURUUTUVSUVIUYEWLXQVAUURUWBUWCVUDUVMXPUUR
      UWBUWCVUDUVMUURUWBUWCVUDUQZUFZUWKUVRUWEUVILZUWFUKRZULUMRZFUNZUOLZUVRVUGUW
      GUKRZULUMRZFUNZUOLZUBRZUVEUVLUCVUEUURUXFUXKUVROUVIPZUQZUWKVUPUCUDUWBUXFUW
      CUXKVUDVUQUXHUXLOUVRUVIVBUXGVDXRUURVURUFZUVRUWEGQZUVRJZHQZVUTUVCLZVUTUVIL
      ZUKRZTUFGHXSZLZUWEVVAVVBVVDVUTUVDLZUKRZTUFGHXSZLZUBRZULUMRZFUNZUOLZUVRVVG
      ULUMRZFUNZUOLZUVRVVKULUMRZFUNZUOLZUBRZUWKVUPUCVUSUURUVROVVFPZUVROVVJPZVVO
      VWBUCUDUURVURXTVURVWCUURVURVVEOJZGUVRUEVWCVURVWEGUVRVURVVAUFZVVCOJZVVDOJZ
      VWEUXFUXKVVAVWGVUQUVROVUTUVCVEYAVUQUXFVVAVWHUXKUVROVUTUVIVEVHZVVCVVDVIVJV
      MGHUVROVVEVVFVVFWAZYBWCYCVURVWDUURVURVVIOJZGUVRUEVWDVURVWKGUVRVWFVWHVVHOJ
      ZVWKVWIUXKUXFVVAVWLVUQUVROVUTUVDVEVGVVDVVHVIVJVMGHUVROVVIVVJVVJWAZYBWCYCF
      AVVFVVJYDYEVUSUWJVVNUOVUSUVRUWIVVMFVUSUXAUFZUWHVVLULUMVWNVVLUWFVUGUKRZVUL
      UBRZUWHUXAVVLVWPTVUSUXAVVGVWOVVKVULUBGHUWEVVEVWOUVRVVFVUTUWETZVVCUWFVVDVU
      GUKVUTUWEUVCYFVUTUWEUVIYFZYGVWJUWFVUGUKVCYHZGHUWEVVIVULUVRVVJVWQVVDVUGVVH
      UWGUKVWRVUTUWEUVDYFYGVWMVUGUWGUKVCYHZYGYCVURUXAVWPUWHTZUURVURUXAUFZUYPVUG
      WRJZUYQVXAVXBUWFUXFUXKUXAUXDVUQUXIYAXBZVXBVUGVUQUXFUXAVUGOJUXKUVROUWEUVIV
      EVHXBZVXBUWGUXKUXFUXAUXEVUQUXMVGXBUWFVUGUWGYIYEYJYKYLYMYNVUSVUKVVRVUOVWAU
      BVUSVUJVVQUOVUSUVRVUIVVPFVURUXAVUIVVPTUURVXBVUHYOZULUMRZVWOULUMRVUIVVPVXB
      VXFVWOULUMVXBVXCUYPVXFVWOTVXEVXDVUGUWFYPVJYLVXBVUHWRJZVXGVUITVXBVXCUYPVXH
      VXEVXDVUGUWFYQVJVUHYRVLVXBVWOVVGULUMUXAVWOVVGTVURUXAVVGVWOVWSYSYCYLYTYJYM
      YNVUSVUNVVTUOVUSUVRVUMVVSFVWNVULVVKULUMUXAVULVVKTVUSUXAVVKVULVWTYSYCYLYMY
      NYGUUAUUBUURUWBUWCUVEUWKTVUDUYJUUCVUFUVJVUKUVKVUOUBUURUWBVUDUVJVUKTZUWCUU
      RVUDUWBVXIUURVUDUWBVXIFAUVIUVCWNUUDUUEUUFUURUWCVUDUVKVUOTZUWBUURVUDUWCVXJ
      UURVUDUWCVXJFAUVIUVDWNUUDUUEUUGYGUUAUUHUUIUUJUUKVPVSUULVTUUSUUMJUVQUVBUVP
      UFUAAKUUNBCDUUMUUSUUTUUTWAUUOUUPUUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M k n $.  $d X k n $.  $d N k n $.  $d A k $.  $d F k n $.  $d G k n $.
    $d R k n $.
    rrndstprj1.1 $e |- M = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    rrndstprj1.2 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    $( The distance between two points in Euclidean space is greater than the
       distance between the projections onto one coordinate. $)
    rrndstprj1 $p |- ( ( ( N e. NN /\ A e. ( 1 ... N ) ) /\ ( F e. X /\ G e. X
                    ) ) -> ( ( F ` A ) M ( G ` A ) ) <_ ( F ( Rn ` N ) G ) ) $=
      ( vk cn wcel c1 cfz co wa cfv cmin cabs cv c2 cexp csu csqr crrn cle wbr
      cuz cr cc0 wral elfzuz2 adantr wf cmap eleq2i reex oprex elmap bitri
      ffvelrn sylanb adantlr adantll resubcl syl11anc resqcl syl sqge0 jca
      r19.21aiva simplr wceq fveq2 opreq12d opreq1d fsumlt1 syl111anc ancoms
      sylan2b ad2ant2lr ad2ant2l absresq fsumrecl fsumcmp0 sqsqr 3brtr4d wb cc
      recn abscl 3syl absge0 sqrcl sqrge0 le2sq syl22anc mpbird remetdval
      simpll biimpi ad2antrl ad2antll rrnmval ) EJKZALEMNZKZOZBFKZCFKZOZOZABPZA
      CPZQNZRPZXEISZBPZXPCPZQNZTUANZIUBZUCPZXLXMDNZBCEUDPNZUEXKXOYBUEUFZXOTUANZ
      YBTUANZUEUFZXKXNTUANZYAYFYGUEXKELUGPKZXTUHKZUIXTUEUFZOZIXEUJZXFYIYAUEUFXG
      YJXJJALEUKULZXKYMIXEXKXPXEKZOZYKYLYQXSUHKZYKYQXQUHKZXRUHKZYRXJYPYSXGXHYPY
      SXIXHXEUHBUMZYPYSXHBUHXEUNNZKZUUAFUUBBHUOZUHXEBUPLEMUQZURUSZXEUHXPBUTVAVB
      VCXJYPYTXGXIYPYTXHXIXEUHCUMZYPYTXICUUBKZUUGFUUBCHUOZUHXECUPUUEURUSZXEUHXP
      CUTVAVCVCXQXRVDVEZXSVFVGZYQYRYLUUKXSVHVGVIVJZXDXFXJVKXTYIIALEXPAVLZXSXNTU
      AUUNXQXLXRXMQXPABVMXPACVMVNVOVPVQXKXNUHKZYFYIVLXKXLUHKZXMUHKZUUOXFXHUUPXD
      XIXHXFUUAUUPUUFUUAXFUUPXEUHABUTVRVSVTZXFXIUUQXDXHXIXFUUGUUQUUJUUGXFUUQXEU
      HACUTVRVSWAZXLXMVDVEZXNWBVGXKYAUHKZUIYAUEUFZYGYAVLXKYJYKIXEUJUVAYOXKYKIXE
      UULVJXTILEWCVEZXKYJYNUVBYOUUMXTILEWDVEZYAWEVEWFXKXOUHKZUIXOUEUFZYBUHKZUIY
      BUEUFZYEYHWGXKUUOXNWHKZUVEUUTXNWIZXNWJWKXKUUOUVIUVFUUTUVJXNWLWKXKUVAUVBUV
      GUVCUVDYAWMVEXKUVAUVBUVHUVCUVDYAWNVEXOYBWOWPWQXKUUPUUQYCXOVLUURUUSXLXMDGW
      RVEXKXDUUCUUHYDYBVLXDXFXJWSXHUUCXGXIXHUUCUUDWTXAXIUUHXGXHXIUUHUUIWTXBIEBC
      XCVQWF $.
      $( [2-Sep-2009] $)

    $( Bound on the distance between two points in Euclidean space given bounds
       on the distances in each coordinate.  This theorem and ~ rrndstprj1 can
       be used to show that the supremum norm and Euclidean norm are
       equivalent. $)
    rrndstprj2 $p |- ( ( ( N e. NN /\ F e. X /\ G e. X ) /\ ( R e. RR+ /\
                    A. n e. ( 1 ... N ) ( ( F ` n ) M ( G ` n ) ) < R ) ) ->
                                ( F ( Rn ` N ) G ) < ( R x. ( sqr ` N ) ) ) $=
      ( vk cn wcel w3a cr c1 cfz co cmap crp cv cfv clt wbr wral wa crrn csqr
      cmul biid eleq2i 3anbi123i cmin c2 cexp csu wceq rrnmval adantr cuz
      elnnuz biimpi 3ad2ant1 wf reex oprex elmap ffvelrn sylanb 3ad2antl2
      3ad2antl3 resubcl syl11anc resqcl syl adantlr rpre ad2antlr wi fveq2
      opreq12d breq1d rcla4v imdistani ancoms cabs remetdval opreq1d absresq
      eqtrd ad2ant2r cc0 cle wb cme remet a1i remetba metcl syl111anc metge0
      rpge0 lt2sq syl22anc biimpa anasss eqbrtrrd sylan2 expr imp 3jca
      r19.21aiva fsumlt caddc cc rpcn sqcl fsumconst syl2an nncn ax1cn npcan
      sylancl 3ad2antl1 adantrr breqtrd fsumrecl sqge0 jca fsumcmp0 sqsqr nnre
      cn0 nnnn0 nn0ge0 sqrcl recnd sqmul mulcom 3eqtr3d 3brtr4d sqrge0 remulcl
      mulge0 mpbird eqbrtrd ) FKLZCGLZDGLZMUUFCNOFPQZRQZLZDUUJLZMZASLZBTZCUAZUU
      ODUAZEQZAUBUCZBUUIUDZUEZCDFUFUAQZAFUGUAZUHQZUBUCUUFUUFUUGUUKUUHUULUUFUIGU
      UJCIUJGUUJDIUJUKUUMUVAUEZUVBUUIJTZCUAZUVFDUAZULQZUMUNQZJUOZUGUAZUVDUBUUMU
      VBUVLUPUVAJFCDUQURUVEUVLUVDUBUCZUVLUMUNQZUVDUMUNQZUBUCZUVEUVKFAUMUNQZUHQZ
      UVNUVOUBUVEUVKUUIUVQJUOZUVRUBUVEFOUSUALZUVJNLZUVQNLZUVJUVQUBUCZMZJUUIUDUV
      KUVSUBUCUUMUVTUVAUUFUUKUVTUULUUFUVTFUTVAZVBZURUVEUWDJUUIUVEUVFUUILZUEUWAU
      WBUWCUUMUWGUWAUVAUUMUWGUEZUVINLZUWAUWHUVGNLZUVHNLZUWIUUKUUFUWGUWJUULUUKUU
      INCVCUWGUWJNUUICVDOFPVEZVFUUINUVFCVGVHVIZUULUUFUWGUWKUUKUULUUINDVCUWGUWKN
      UUIDVDUWLVFUUINUVFDVGVHVJZUVGUVHVKVLZUVIVMVNZVOUVAUWBUUMUWGUUNUWBUUTUUNAN
      LZUWBAVPZAVMVNURVQUVEUWGUWCUUMUUNUUTUWGUWCVRUUMUUNUEZUUTUWGUWCUUTUWGUEUWS
      UWGUVGUVHEQZAUBUCZUEZUWCUWGUUTUXBUWGUUTUXAUUSUXABUVFUUIUUOUVFUPZUURUWTAUB
      UXCUUPUVGUUQUVHEUUOUVFCVSUUOUVFDVSVTWAWBWCWDUWSUXBUEUWTUMUNQZUVJUVQUBUUMU
      WGUXDUVJUPUUNUXAUWHUXDUVIWEUAZUMUNQZUVJUWHUWTUXEUMUNUWHUWJUWKUWTUXEUPUWMU
      WNUVGUVHEHWFVLWGUWHUWIUXFUVJUPUWOUVIWHVNWIWJUWSUWGUXAUXDUVQUBUCZUWSUWGUEZ
      UXAUXGUXHUWTNLZWKUWTWLUCZUWQWKAWLUCZUXAUXGWMUXHEWNLZUWJUWKUXIUXLUXHEHWOWP
      ZUUMUWGUWJUUNUWMVOZUUMUWGUWKUUNUWNVOZUVGUVHENEHWQZWRWSUXHUXLUWJUWKUXJUXMU
      XNUXOUVGUVHENUXPWTWSUUNUWQUUMUWGUWRVQUUNUXKUUMUWGAXAZVQUWTAXBXCXDXEXFXGXH
      XEXIXJXKUVJUVQJOFXLVLUUMUUNUVSUVRUPZUUTUUFUUKUUNUXRUULUUFUUNUEZUVSFOULQOX
      MQZUVQUHQZUVRUUFUVTUVQXNLZUVSUYAUPUUNUWEUUNAXNLZUYBAXOZAXPVNUVQJOFXQXRUXS
      UXTFUVQUHUUFUXTFUPZUUNUUFFXNLOXNLUYEFXSXTFOYAYBURWGWIYCYDYEUUMUVNUVKUPZUV
      AUUMUVKNLZWKUVKWLUCZUYFUUMUVTUWAJUUIUDUYGUWFUUMUWAJUUIUWPXKUVJJOFYFVLZUUM
      UVTUWAWKUVJWLUCZUEZJUUIUDUYHUWFUUMUYKJUUIUWHUWAUYJUWPUWHUWIUYJUWOUVIYGVNY
      HXKUVJJOFYIVLZUVKYJVLURUUMUUNUVOUVRUPZUUTUUFUUKUUNUYMUULUXSUVCAUHQZUMUNQZ
      UVCUMUNQZUVQUHQZUVOUVRUUFUVCXNLZUYCUYOUYQUPUUNUUFUVCUUFFNLZWKFWLUCZUVCNLZ
      FYKZUUFFYLLUYTFYMFYNVNZFYOVLZYPZUYDUVCAYQXRUXSUYNUVDUMUNUUFUYRUYCUYNUVDUP
      UUNVUEUYDUVCAYRXRWGUXSUYPFUVQUHUUFUYPFUPZUUNUUFUYSUYTVUFVUBVUCFYJVLURWGYS
      YCYDYTUUMUUNUVMUVPWMZUUTUWSUVLNLZWKUVLWLUCZUVDNLZWKUVDWLUCZVUGUUMVUHUUNUU
      MUYGUYHVUHUYIUYLUVKYOVLURUUMVUIUUNUUMUYGUYHVUIUYIUYLUVKUUAVLURUUFUUKUUNVU
      JUULUUNUUFVUJUUNUWQVUAVUJUUFUWRVUDAUVCUUBXRWDYCUUFUUKUUNVUKUULUUNUUFVUKUU
      NUWQUXKUEVUAWKUVCWLUCZUEVUKUUFUUNUWQUXKUWRUXQYHUUFVUAVULVUDUUFUYSUYTVULVU
      BVUCFUUAVLYHAUVCUUCXRWDYCUVLUVDXBXCYDUUDUUEVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N f g j k m n r s x y z $.
    $( Euclidean space is complete. $)
    rrncms $p |- ( N e. NN -> ( Rn ` N ) e. CMet ) $=
      ( vf vx vm vz vn vy vj vk vr vg vs cn wcel crrn cfv cme cdm cv wf clm
      wbr wrex wi cca wral cms rrnmet wa wex c1 cfz co cr wceq copab cabs cmin
      ccom cxp cres cle clt crp ffvelrn adantrr adantrl jca adantlr adantll
      cmap rrndm adantr eleq2d anbi12d biimpd imdistani eqid rrndstprj1 syl
      adantlrl reex oprex elmap syl6bb expcom sylan9 imp remet remetba metcl
      mp3an1 syl11anc 3expb sylan rpre ad2antlr lelttr syl111anc mpand syldan
      anassrs fveq2 fveq1d fvex fvopab4 adantl opreq12d breq1d sylibrd imim2d
      ralimdva reximdva wb simprl iscau5 syl6rbb biimpar an32s sylan2
      r19.21aiva anasss ancom2s fopab2 sylib sylancr 3imtr4d eqcomi a1i breq1
      rexeqbidv recms iscms mpbi simpri vtoclri breq2 ac6s ad2antrr biimprd
      csqr cdiv biimpa adantllr eqcomd lmbrnns nnrp rpsqrcl rpdivcl ancoms
      imbi2d rexralbidv rcla4v sylbid cfn c0 wne wss cuz elnnuz fzfi sylbi
      ad3antrrr biimpi eluzfz1 ne0i 3syl nnssre filbcmb ex w3a cmul 3ad2ant1
      rrndstprj2 exp32 syld imp32 cc cc0 rpcn nnre cn0 nnnn0 nn0ge0 sqrcl recn
      rpne0 divcan1 3ad2antl1 breqtrd expdimp an4s reximdv 3syld r19.21adva
      eqidd syl3an1 3expa imdistand syland eximdv mpd df-rex sylibr iscms2
      sylanbrc ) AMNZAOPZQNZMUXHRRZBSZTZUXKCSZUXHUAPUBZCUXJUCZUDZBUXHUEPZUFUXHU
      GNAUHZUXGUXPBUXQUXGUXKUXQNZUIZUXLUXOUXTUXLUIZUXMUXJNZUXNUIZCUJZUXOUYAUKAU
      LUMZUNUXMTZDSZMNZESFSZUYGUXKPZPZUOUIDEUPZUYIUXMPZUQURUSUNUNUTVAZUAPZUBZFU
      YEUFZUIZCUJZUYDUYAUYLGSZUYOUBZGUNUCZFUYEUFUYSUYAVUBFUYEUYAUYIUYENZUIUYLUY
      NUEPZNZVUBUXTUXLVUCVUEUXGUXLVUCUIZUXSVUEUXGVUFUIZUXSVUEVUGHSZISZVBUBZVUHU
      XKPZVUIUXKPZUXHUMZJSZVCUBZUDZIMUFZHMUCZJVDUFZVUJVUHUYLPZVUIUYLPZUYNUMZVUN
      VCUBZUDZIMUFZHMUCZJVDUFZUXSVUEVUGVURVVFJVDVUGVUNVDNZUIZVUQVVEHMVVIVUHMNZU
      IZVUPVVDIMVVKVUIMNZUIZVUOVVCVUJVVMVUOUYIVUKPZUYIVULPZUYNUMZVUNVCUBZVVCVVI
      VVJVVLVUOVVQUDZVVIVVJVVLUIZVUKUXJNZVULUXJNZUIZVVRVUGVVSVWBVVHVUFVVSVWBUXG
      UXLVVSVWBVUCUXLVVSUIVVTVWAUXLVVJVVTVVLMUXJVUHUXKVEVFUXLVVLVWAVVJMUXJVUIUX
      KVEZVGVHVIVJVIVVIVWBUIZVVPVUMVBUBZVUOVVQVUGVWBVWEVVHUXGVUCVWBVWEUXLUXGVUC
      UIZVWBUIZVWFVUKUNUYEVKUMZNZVULVWHNZUIZUIVWEVWFVWBVWKVWFVWBVWKVWFVVTVWIVWA
      VWJVWFUXJVWHVUKUXGUXJVWHUOZVUCAVLZVMZVNVWFUXJVWHVULVWNVNVOVPVQUYIVUKVULUY
      NAVWHUYNVRZVWHVRZVSVTWAVIVWDVVPUNNZVUMUNNZVUNUNNZVWEVUOUIVVQUDVUGVWBVWQVV
      HUXGVUCVWBVWQUXLVWGVVNUNNZVVOUNNZVWQVWFVVTVWTVWAVWFVVTVWTUXGVVTUYEUNVUKTZ
      VUCVWTUXGVVTVXBUXGVVTVWIVXBUXGUXJVWHVUKVWMVNUNUYEVUKWBUKAULWCZWDWEVPVXBVU
      CVWTUYEUNUYIVUKVEWFWGWHVFVWFVWAVXAVVTVWFVWAVXAUXGVWAUYEUNVULTZVUCVXAUXGVW
      AVXDUXGVWAVWJVXDUXGUXJVWHVULVWMVNZUNUYEVULWBVXCWDWEVPVXDVUCVXAUYEUNUYIVUL
      VEWFWGWHVGUYNQNZVWTVXAVWQUYNVWOWIZVVNVVOUYNUNUYNVWOWJZWKWLWMWAVIVUGVWBVWR
      VVHUXGVWBVWRVUFUXGUXIVWBVWRUXRUXIVVTVWAVWRVUKVULUXHUXJUXJVRZWKWNWOVIVIVVH
      VWSVUGVWBVUNWPWQVVPVUMVUNWRWSWTXAXBVVMVVBVVPVUNVCVVMVUTVVNVVAVVOUYNVVJVUT
      VVNUOVVIVVLDEVUHUYKVVNMUYLUYGVUHUOUYIUYJVUKUYGVUHUXKXCXDUYLVRZUYIVUKXEXFW
      QVVLVVAVVOUOVVKDEVUIUYKVVOMUYLUYGVUIUOUYIUYJVULUYGVUIUXKXCXDVXJUYIVULXEXF
      ZXGXHXIXJXKXLXMXLVUGUXIUXLUXSVUSXNUXGUXIVUFUXRVMUXGUXLVUCXOJUXHHIUXKUXJVX
      IXPWMVUGVXFMUNUYLTZVUEVVGXNVXGVUGUYKUNNZDMUFZVXLUXGVUCUXLVXNUXGVUCUXLVXNV
      WFUXLUIVXMDMVWFUXLUYHVXMUXLUYHUIZVWFUYJUXJNZVXMMUXJUYGUXKVEZUXGVXPVUCVXMU
      XGVXPUIUYEUNUYJTZVUCVXMUXGVXRVXPUXGVXPUYJVWHNVXRUXGUXJVWHUYJVWMVNUNUYEUYJ
      WBVXCWDXQXRZUYEUNUYIUYJVEZWOXSXTXBYAYBYCDEMUNUYKUYLVXJYDZYEJUYNHIUYLUNVXH
      XPYFYGWHXSXBKSZUYTUYOUBZGUYNRRZUCZVUBKUYLVUDVYBUYLUOZVYCVUAGVYDUNVYDUNUOV
      YFUNVYDVXHYHYIVYBUYLUYTUYOYJYKVXFVYEKVUDUFZUYNUGNVXFVYGUIYLGUYNKVYDVYDVRY
      MYNYOYPVTYAVUAUYPFGUYEUNCVXCUYTUYMUYLUYOYQYRVTUYAUYRUYCCUYAUYFUYBUYQUYCUY
      AUYBUYFUYAUYBUXMVWHNZUYFUYAUXJVWHUXMUXGVWLUXSUXLVWMYSVNUNUYEUXMWBVXCWDZWE
      YTUYAUYBUYQUXNUXGUXLUYBUYQUXNUDZUDUXSUXGUXLUIZUYBVYJVYKUYBUIZUYQVUJVULUXM
      UXHUMZVUNVCUBZUDZIMUFZHMUCZJVDUFZUXNVYLUYQVYQJVDVYLVVHUIZUYQVUJVVOUYMUYNU
      MZVUNAUUAPZUUBUMZVCUBZUDZIMUFHMUCZFUYEUFZVUJWUCFUYEUFZUDZIMUFZHMUCZVYQVYS
      UYPWUEFUYEVYSVUCUIZUYPVUJVYTLSZVCUBZUDZIMUFHMUCZLVDUFZWUEWUKVXFUYMUNNZVXL
      UYPWUPXNVXFWUKVXGYIVYLVUCWUQVVHUXGUYBVUCWUQUXLUXGUYBUIZUYFVUCWUQWURVYHUYF
      UXGUYBVYHUXGUXJVWHUXMVWMVNZUUCVYIYEUYEUNUYIUXMVEWOUUDVIWUKVXNVXLVYLVUCVXN
      VVHVYKVUCVXNUYBVYKVUCUIVXMDMVYKUYHVUCVXMVYKUYHUIVXRVUCVXMUXGUXLUYHVXRVXOU
      XGVXPVXRVXQVXSXTXBVXTWOXSYAVIVIVYAYELVVOUYNUYMHIUYLUNVXHVVLVVAVVOVXKUUEUU
      FWSVYSWUPWUEUDZVUCVYKVVHWUTUYBUXGVVHWUTUXLUXGVVHUIZWUBVDNZWUTVVHUXGWVBUXG
      VVHWUAVDNZWVBUXGAVDNZWVCAUUGZAUUHZVTVUNWUAUUIXTZUUJWUOWUELWUBVDWULWUBUOZW
      UNWUDHIMMWVHWUMWUCVUJWULWUBVYTVCYQUUKUULUUMVTVIVIVMUUNXLVYSUYEUUONZUYEUUP
      UUQZMUNUURZWUFWUJUDUXGWVIUXLUYBVVHUXGAUKUUSPNZWVIAUUTZUKAUVAUVBUVCUXGWVJU
      XLUYBVVHUXGWVLUKUYENWVJUXGWVLWVMUVDUKAUVEUYEUKUVFUVGUVCWVKVYSUVHYIWUCFHIU
      YEMUVIWSVYSWUIVYPHMVYSWUHVYOIMVYSVVLUIZWUGVYNVUJWVNVYSVWAUIWUGVYNUDZVYSVV
      LVWAVYKVVLVWAUDZUYBVVHUXLWVPUXGUXLVVLVWAVWCUVJXGYSVQVYLVVHVWAWVOUXGUYBVVH
      VWAUIWVOUXLUXGVVHUYBVWAWVOUXGUYBVWAUIZVVHWVOUXGWVQUIUXGVWJVYHUIZUIZVVHWVO
      UXGVWAUYBWVSUXGVWAUYBUIZWVRUXGWVTWVRUXGVWAVWJUYBVYHVXEWUSVOVPVQYCWVSVVHWU
      GVYNUXGVWJVYHVVHWUGUIZVYNUDUXGVWJVYHUVKZWWAVYNWWBWWAUIVYMWUBWUAUVLUMZVUNV
      CWWBVVHWUGVYMWWCVCUBZWWBVVHWVBWUGWWDUDUXGVWJVVHWVBUDVYHVVHUXGWVBWVGWFUVMW
      WBWVBWUGWWDWUBFVULUXMUYNAVWHVWOVWPUVNUVOUVPUVQWWBVVHWWCVUNUOZWUGUXGVWJVVH
      WWEVYHWVAVUNUVRNZWUAUVRNZWUAUVSUUQZWWEVVHWWFUXGVUNUVTXGUXGWWGVVHUXGAUNNZU
      VSAVBUBZUIWUAUNNWWGUXGWWIWWJAUWAUXGAUWBNWWJAUWCAUWDVTVHAUWEWUAUWFUVGVMUXG
      WWHVVHUXGWVDWVCWWHWVEWVFWUAUWGUVGVMVUNWUAUWHWSUWIVFUWJUVJWNUWKWOXSUWLUUDX
      BVTXKXLUWMUWNUWOUXGUYBUXLUXNVYRXNZUXGUYBUXLWWKUXGUXIUYBUXLWWKUXRJVULUXHUX
      MHIUXKUXJVXIVVLVULUWPUUFUWQUWRXSXJUVJVIUWSUWTUXAUXBUXNCUXJUXCUXDUVJYACUXH
      BUXJVXIUXEUXF $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X a b d f m n r u v w x y z $.  $d M a b d f m n r u v w x y z $.
    $d Y a b d f m n r u v w x y z $.  $d N a b d f m n r u v w x y z $.
    rrntotbnd.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrntotbnd.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem1 $p |- ( ( ( ( N e. NN /\ x e. X ) /\ ( d e. RR+ /\ r e. RR+ )
          ) /\ ( y e. dom dom ( Rn ` N ) /\ ( x ( Rn ` N ) y ) < r ) ) ->
        ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
            ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\
            v = ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( ball ` ( Rn ` N ) ) d ) e.
        { a | E. f ( f : ( 1 ... N ) --> ( -u ( ( |_ ` ( r / ( d / ( sqr ` N )
            ) ) ) + 1 ) ... ( ( |_ ` ( r / ( d / ( sqr ` N ) ) ) ) + 1 ) ) /\
                a = ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
                                ( ( d / ( sqr ` N ) ) x. ( f ` m ) ) ) ) }
                                            ( ball ` ( Rn ` N ) ) d ) ) } ) $=
      ( cn wcel cv wa crp crrn cfv cdm co clt wbr c1 cfz csqr cdiv cfl caddc
      cneg wf cmin c2 wceq copab cmul cbl wex cab wral cz cabs cle cn0 simplr
      nnrp rpsqrcl syl rpdivcl sylan2 ancoms adantlr syl11anc cr cc0 rpre
      rpge0 flge0nn0 nn0p1nn 3syl an31s ad2antrr wne cmap rrndm eleq2d reex
      oprex elmap syl6bb biimpa ffvelrn sylan adantllr eleq2i bitri sylanb
      adantll resubcl ad2ant2r rpne0 redivcl syl111anc 2re 2ne0 rereccli
      readdcl sylancl flcl adantlrr cc zcn abscl recnd peano2zdi zre anasss
      peano2re absrdbnd absdiv absid opreq2d eqtrd recn cme rrnmet syl6reqr
      adantr simpr eqid metcl ad2antll ccom cxp cres remetdval remet a1i
      remetba metsym wi biimprd imdistani rrndstprj1 an32s ex anassrs imp
      eqbrtrd eqbrtrrd adantl ad2antlr ltle impr letrd wb elrp sylib lediv1
      mpbid flwordi 1re leadd1 w3a nnre absle syl2an biimp3a simp2 nnnegz
      3ad2ant1 nnz elfz mpbird r19.21aiva fopab2 eqidd opabex2 feq1 fveq1
      eqeq2d anbi2d opabbidv opreq1d anbi12d cla4ev eqeq1 exbidv elab sylibr )
      IQRZASZJRZTZNSZUARZLSZUARZTZTZBSZIUBUCZUDUDZRZUWPUXEUXFUEZUXAUFUGZTZTZUHI
      UIUEZUXAUWSIUJUCZUKUEZUKUEZULUCZUHUMUEZUNZUXRUIUEZESZUOZFSZUXMRZDSZUYCUWP
      UCZUXOUYCGSZUXMRZCSUYGUXEUCZUYGUWPUCZUPUEZUXOUKUEZUHUQUKUEZUMUEZULUCZURTG
      CUSZUCZUTUEZUMUEZURZTZFDUSZUWSUXFVAUCZUEZUYDUYEUYFUXOUYCUYAUCZUTUEZUMUEZU
      RZTZFDUSZUWSVUCUEZURZTZEVBZVUDUYBMSZVUKURZTZEVBZMVCRUXLUXMUXTUYPUOZVUDVUD
      URZVUNUXLUYOUXTRZGUXMVDVUSUXLVVAGUXMUXLUYHTZUXRQRZUYOVERZUYOVFUCZUXRVGUGZ
      VVAUXDVVCUXKUYHUWOUXCVVCUWQUXCUWOVVCUWOUXBUWTVVCUWOUXBTUWTTZUXPUARZUXQVHR
      ZVVCVVGUXBUXOUARZVVHUWOUXBUWTVIUWOUWTVVJUXBUWTUWOVVJUWOUWTUXNUARZVVJUWOIU
      ARVVKIVJIVKVLUWSUXNVMVNZVOZVPUXAUXOVMZVQVVHUXPVRRZVSUXPVGUGVVIUXPVTZUXPWA
      UXPWBVQUXQWCWDWEVOVPWFUXDUXHUYHVVDUXJUXDUXHTZUYHTZUYNVRRZVVDVVRUYLVRRZUYM
      VRRVVSVVRUYKVRRZUXOVRRZUXOVSWGZVVTUWRUXHUYHVWAUXCUWRUXHTZUYHTZUYIVRRZUYJV
      RRZVWAUWOUXHUYHVWFUWQUWOUXHTUXMVRUXEUOZUYHVWFUWOUXHVWHUWOUXHUXEVRUXMWHUEZ
      RVWHUWOUXGVWIUXEIWIZWJVRUXMUXEWKUHIUIWLZWMWNWOUXMVRUYGUXEWPWQWRZUWRUYHVWG
      UXHUWQUYHVWGUWOUWQUXMVRUWPUOZUYHVWGUWQUWPVWIRVWMJVWIUWPOWSVRUXMUWPWKVWKWM
      WTUXMVRUYGUWPWPXAXBVPZUYIUYJXCVQWRZUXDVWBUXHUYHUWOUWTVWBUWQUXBUWTUWOVWBUW
      TUWOTZVVJVWBVVLUXOVTVLVOZXDWFUXDVWCUXHUYHUWOUWTVWCUWQUXBUWTUWOVWCVWPVVJVW
      CVVLUXOXEVLVOXDZWFUYKUXOXFXGZUQXHXIXJUYLUYMXKXLZUYNXMZVLXNVVBVVEUYLVFUCZU
      LUCZUHUMUEZUXRVVBUYOXORZVVEVRRVVBVVSVVDVXEUXDUXHUYHVVSUXJVWTXNVXAUYOXPWDU
      YOXQVLVVBVXBVRRZVXDVERVXDVRRUXDUXHUYHVXFUXJVVRUYLXORVXFVVRUYLVWSXRUYLXQVL
      XNZVXFVXCVXBXMZXSVXDXTWDVVBUXQVRRZUXRVRRZUXDVXIUXKUYHUWOUXCVXIUWQUWOUXCTV
      VOUXQVERVXIUWOUWTUXBVVOUXBUWOUWTTZVVOUXBVXKTVVHVVOVXKUXBVVJVVHVVMVVNVNVVP
      VLVOYAZUXPXMUXQXTWDVPWFZUXQYBVLVVBVVTVVEVXDVGUGUXDUXHUYHVVTUXJVWSXNUYLYCV
      LVVBVXCUXQVGUGZVXDUXRVGUGZVVBVXFVVOVXBUXPVGUGVXNVXGUXDVVOUXKUYHUWOUXCVVOU
      WQVXLVPWFVVBVXBUYKVFUCZUXOUKUEZUXPVGVVBVXBVXPUXOVFUCZUKUEZVXQVVBUYKXORZUX
      OXORZVWCVXBVXSURVVBUYKUXDUXHUYHVWAUXJVWOXNZXRUXDVYAUXKUYHUWOUWTVYAUWQUXBV
      XKUXOVWQXRXDWFUXDVWCUXKUYHVWRWFUYKUXOYDXGVVBVXRUXOVXPUKUXDVXRUXOURZUXKUYH
      UWOUWTVYCUWQUXBVXKVWBVSUXOVGUGZVYCVWQVXKVVJVYDVVMUXOWAVLUXOYEVQXDWFYFYGVV
      BVXPUXAVGUGZVXQUXPVGUGZVVBVXPUXIUXAVVBVWAVXTVXPVRRZVYBUYKYHUYKXQWDZUXLUXI
      VRRZUYHUWRUXHVYIUXCUXJVWDUXFYIRZUWPUXGRZUXHVYIUWOVYJUWQUXHIYJWFUWRVYKUXHU
      WOUWQVYKUWOJUXGUWPUWOUXGVWIJVWJOYKZWJWOYLUWRUXHYMUWPUXEUXFUXGUXGYNYOXGZXD
      YLUXDUXAVRRZUXKUYHUXBVYNUWRUWTUXAVTZYPWFZUWRUXKUYHVXPUXIVGUGZUXCUWRUXHUYH
      VYQUXJVWEUYIUYJVFUPYQVRVRYRYSZUEZVXPUXIVGVWEVWFVWGVYSVXPURVWLVWNUYIUYJVYR
      VYRYNZYTVQVWEVYSUYJUYIVYRUEZUXIVGVWEVYRYIRZVWFVWGVYSWUAURWUBVWEVYRVYTUUAU
      UBVWLVWNUYIUYJVYRVRVYRVYTUUCUUDXGVWDUWRUXEJRZTZUYHWUAUXIVGUGZUWRUXHWUCUWO
      UXHWUCUUEUWQUWOWUCUXHUWOJUXGUXEVYLWJUUFYLUUGWUDUYHWUEUWOUWQWUCUYHWUEUUEUW
      OUWQWUCTZTUYHWUEUWOUYHWUFWUEUYGUWPUXEVYRIJVYTOUUHUUIUUJUUKUULWQUUMUUNXNWR
      UXLUXIUXAVGUGZUYHUXDUXHUXJWUGVVQVYIVYNUXJWUGUUEUWRUXHVYIUXCVYMVPUXCVYNUWR
      UXHUXBVYNUWTVYOUUOUUPUXIUXAUUQVQUURYLUUSVVBVYGVYNVWBVSUXOUFUGTZVYEVYFUUTV
      YHVYPVVBVVJWUHUXDVVJUXKUYHUWOUWTVVJUWQUXBVVMXDWFUXOUVAUVBVXPUXAUXOUVCXGUV
      DUUMVXBUXPUVEXGVVBVXCVRRZVXIUHVRRZVXNVXOUUTVVBVXFVXCVERWUIVXGVXHVXCXTWDVX
      MWUJVVBUVFUUBVXCUXQUHUVGXGUVDUUSVVCVVDVVFUVHZVVAUXSUYOVGUGUYOUXRVGUGTZVVC
      VVDVVFWULVVDVVCVVFWULUUTZVVDUYOVRRVXJWUMVVCUYOXTUXRUVIUYOUXRUVJUVKVOUVLWU
      KVVDUXSVERZUXRVERZVVAWULUUTVVCVVDVVFUVMVVCVVDWUNVVFUXRUVNUVOVVCVVDWUOVVFU
      XRUVPUVOUYOUXSUXRUVQXGUVRXGUVSGCUXMUXTUYOUYPUYPYNUVTUVBUXLVUDUWAVUMVUSVUT
      TEUYPGCUXMUYOVWKUWBUYAUYPURZUYBVUSVULVUTUXMUXTUYAUYPUWCWUPVUKVUDVUDWUPVUJ
      VUBUWSVUCWUPVUIVUAFDWUPVUHUYTUYDWUPVUGUYSUYEWUPVUFUYRUYFUMWUPVUEUYQUXOUTU
      YCUYAUYPUWDYFYFUWEUWFUWGUWHUWEUWIUWJVQVURVUNMVUDVUBUWSVUCWLVUOVUDURZVUQVU
      MEWUQVUPVULUYBVUOVUDVUKUWKUWFUWLUWMUWN $.
      $( [2-Sep-2009] $)

    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem2 $p |- ( ( ( N e. NN /\ d e. RR+ ) /\
          ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) ) ->
                  ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
        ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\ v =
                ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                      ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( Rn ` N ) y ) < d ) $=
      ( va cn wcel cv crp wa cr c1 cfz co cmap cfv csqr cdiv cmin c2 caddc cfl
      wceq copab cmul crrn clt cabs ccom cxp cres wbr wral simpll wf reex
      oprex elmap ffvelrn sylanb adantll adantlrr nnrp rpsqrcl syl rpdivcl
      sylan2 rpre ancoms ad2antrr cz cc0 wne adantlr resubcl syl11anc rpne0
      redivcl syl111anc 2re 2ne0 rereccli readdcl sylancl flcl zre 3syl
      r19.21aiva eqid fopab2 sylib sylan remulcl sylibr simprr adantr fveq2
      opreq2d opreq12d fvopab4 opreq1d adantl fveq2d fvex jca simprl remetdval
      cc recnd subcl abscl a1i 1re cle rddif halflt1 lelttrd wb rpgt0 ltmul2
      syl112anc mpbid recn ad2antrl addcom ad2antll subsub3 rpcn divcan2
      eqcomd subdi eqtr4d 3eqtr2d absmul rpge0 absid 3eqtrd mulid1 3brtr4d
      eqbrtrd syl2an anassrs rrndstprj2 syl32anc divcan1 breqtrd ) HOPZKQZRPZSZ
      AQZTUAHUBUCZUDUCZPZBQZUVBPZSZSZEQZUVAPZDQUVHUUTUEZUUQHUFUEZUGUCZUVHFQZUVA
      PZCQUVMUVDUEZUVMUUTUEZUHUCZUVLUGUCZUAUIUGUCZUJUCZUKUEZULSFCUMZUEZUNUCZUJU
      CZULSEDUMZUVDHUOUEUCZUVLUVKUNUCZUUQUPUVGUUPUWFUVBPZUVEUVLRPZNQZUWFUEZUWKU
      VDUEZUQUHURTTUSUTZUCZUVLUPVAZNUVAVBUWGUWHUPVAUUPUURUVFVCUVGUVATUWFVDZUWIU
      VGUWETPZEUVAVBUWQUVGUWREUVAUVGUVISZUVJTPZUWDTPZUWRUUSUVCUVIUWTUVEUVCUVIUW
      TUUSUVCUVATUUTVDZUVIUWTTUVAUUTVEUAHUBVFZVGZUVATUVHUUTVHVIVJVKUWSUVLTPZUWC
      TPZUXAUUSUXEUVFUVIUURUUPUXEUURUUPSZUWJUXEUUPUURUVKRPZUWJUUPHRPZUXHHVLZHVM
      ZVNUUQUVKVOVPZUVLVQZVNVRZVSUVGUVATUWBVDZUVIUXFUVGUWATPZFUVAVBUXOUVGUXPFUV
      AUVGUVNSZUVTTPZUWAVTPUXPUXQUVRTPZUVSTPZUXRUXQUVQTPZUXEUVLWAWBZUXSUVFUVNUY
      AUUSUVFUVNSUVOTPZUVPTPZUYAUVEUVNUYCUVCUVEUVATUVDVDZUVNUYCTUVAUVDVEUXCVGZU
      VATUVMUVDVHVIVJUVCUVNUYDUVEUVCUXBUVNUYDUXDUVATUVMUUTVHVIWCUVOUVPWDWEVJUUS
      UXEUVFUVNUXNVSUUSUYBUVFUVNUURUUPUYBUXGUWJUYBUXLUVLWFZVNVRVSUVQUVLWGWHUIWI
      WJWKZUVRUVSWLWMUVTWNUWAWOWPWQFCUVATUWAUWBUWBWRZWSWTUVATUVHUWBVHXAUVLUWCXB
      WEUVJUWDWLWEWQEDUVATUWEUWFUWFWRZWSWTTUVAUWFVEUXCVGXCUUSUVCUVEXDUUSUWJUVFU
      URUUPUWJUXLVRZXEUVGUWPNUVAUVGUWKUVAPZSZUWOUWKUUTUEZUVLUWKUWBUEZUNUCZUJUCZ
      UWMUWNUCZUVLUPUYLUWOUYRULUVGUYLUWLUYQUWMUWNEDUWKUWEUYQUVAUWFUVHUWKULZUVJU
      YNUWDUYPUJUVHUWKUUTXFUYSUWCUYOUVLUNUVHUWKUWBXFXGXHUYJUYNUYPUJVFXIXJXKUYMU
      YRUYNUVLUWMUYNUHUCZUVLUGUCZUVSUJUCZUKUEZUNUCZUJUCZUWMUWNUCZUVLUPUYLUYRVUF
      ULUVGUYLUYQVUEUWMUWNUYLUYPVUDUYNUJUYLUYOVUCUVLUNFCUWKUWAVUCUVAUWBUVMUWKUL
      ZUVTVUBUKVUGUVRVUAUVSUJVUGUVQUYTUVLUGVUGUVOUWMUVPUYNUHUVMUWKUVDXFUVMUWKUU
      TXFXHXJXJXLUYIVUBUKXMXIXGXGXJXKUUSUVFUYLVUFUVLUPVAZUUSUWJUYNTPZUWMTPZSZVU
      HUVFUYLSZUYKVULVUIVUJUVCUYLVUIUVEUVCUXBUYLVUIUXDUVATUWKUUTVHVIWCUVEUYLVUJ
      UVCUVEUYEUYLVUJUYFUVATUWKUVDVHVIVJXNUWJVUKSZVUFVUEUWMUHUCZUQUEZUVLUPVUMVU
      ETPZVUJVUFVUOULVUMVUIVUDTPZVUPUWJVUIVUJXOVUMUXEVUCTPZVUQUWJUXEVUKUXMXEZVU
      MVUBTPZVUCVTPVURVUMVUATPZUXTVUTVUMUYTTPZUXEUYBVVAVUKVVBUWJVUJVUIVVBUWMUYN
      WDVRXKZVUSUWJUYBVUKUYGXEZUYTUVLWGWHZUYHVUAUVSWLWMVUBWNVUCWOWPZUVLVUCXBWEZ
      UYNVUDWLWEUWJVUIVUJXDVUEUWMUWNUWNWRZXPWEVUMUVLVUCVUAUHUCZUQUEZUNUCZUVLUAU
      NUCZVUOUVLUPVUMVVJUAUPVAZVVKVVLUPVAZVUMVVJUVSUAVUMVVIXQPZVVJTPZVUMVUCXQPZ
      VUAXQPZVVOVUMVUCVVFXRZVUMVUAVVEXRZVUCVUAXSWEZVVIXTVNZUXTVUMUYHYAUATPZVUMY
      BYAZVUMVVAVVJUVSYCVAVVEVUAYDVNUVSUAUPVAVUMYEYAYFVUMVVPVWCUXEWAUVLUPVAZVVM
      VVNYGVWBVWDVUSUWJVWEVUKUVLYHXEVVJUAUVLYIYJYKVUMVUOUVLVVIUNUCZUQUEZUVLUQUE
      ZVVJUNUCZVVKVUMVUNVWFUQVUMVUNVUDUYNUJUCZUWMUHUCZVUDUYTUHUCZVWFVUMVUEVWJUW
      MUHVUMUYNXQPZVUDXQPZVUEVWJULVUIVWMUWJVUJUYNYLYMZVUMVUDVVGXRZUYNVUDYNWEXJV
      UMVWNUWMXQPZVWMVWLVWKULVWPVUJVWQUWJVUIUWMYLYOVWOVUDUWMUYNYPWHVUMVWLVUDUVL
      VUAUNUCZUHUCZVWFVUMUYTVWRVUDUHVUMVWRUYTVUMUYTXQPUVLXQPZUYBVWRUYTULVUMUYTV
      VCXRUWJVWTVUKUVLYQZXEZVVDUYTUVLYRWHYSXGVUMVWTVVQVVRVWFVWSULVXBVVSVVTUVLVU
      CVUAYTWHUUAUUBXLVUMVWTVVOVWGVWIULVXBVWAUVLVVIUUCWEVUMVWHUVLVVJUNVUMUXEWAU
      VLYCVAZVWHUVLULVUSUWJVXCVUKUVLUUDXEUVLUUEWEXJUUFVUMVVLUVLUWJVVLUVLULZVUKU
      WJVWTVXDVXAUVLUUGVNXEYSUUHUUIUUJUUKUUIUUIWQUVLNUWFUVDUWNHUVBVVHUVBWRUULUU
      MUUSUWHUUQULZUVFUUSUUQXQPZUVKXQPZUVKWAWBZVXEUURVXFUUPUUQYQXKUUPVXGUURUUPU
      XIUXHVXGUXJUXKUVKYQWPXEUUPVXHUURUUPUXIUXHVXHUXJUXKUVKWFWPXEUUQUVKUUNWHXEU
      UO $.
      $( [2-Sep-2009] $)

    $( A set in Euclidean space is totally bounded iff its is bounded. $)
    rrntotbnd $p |- ( ( N e. NN /\ Y C_ X ) ->
                                      ( M e. TotBnd <-> M e. Bnd ) ) $=
      ( vx vr vv vb vy vd vf va vm vu vz vn cn wcel wss wa ctotbnd cbnd cdm
      eqid totbndbnd cv cbl cfv co wceq crp wrex wral cuni crrn cfn cxp cres
      a1i dmeqd cme rrnmet adantr cr c1 cfz cmap rrndm syl6eqr sseq2d biimpar
      metssba2 syl11anc eqtr4d raleqdv c0 wi 0fin id 0ss eqsstrd adantl ral0
      jctir unieq raleq anbi12d rcla4ev sylancr r19.21aiva a1d wn wne df-ne
      r19.2z csqr cdiv cfl caddc cneg wf cmul copab wex cab ssel2 anim1i
      anasss anim2i anassrs clt wbr wb cc0 syl6reqr eleq2d biimpa jca rpregt0
      elbl syl2an adantrl cmin c2 rrntotbndlem1 biimpd ad2antrr imdistani
      eleq2i biimpi rrntotbndlem2 reex oprex elmap ffvelrn sylanb adantll
      adantlrr nnrp rpsqrcl syl rpdivcl sylan2 rpre ancoms cz adantlr resubcl
      rpne0 redivcl syl111anc 2re 2ne0 rereccli readdcl sylancl flcl zre 3syl
      fopab2 sylib sylan remulcl syl6bb mpbird ad2ant2rl ad2antlr elbl2
      syl31anc expr an32s imp sylanl1 adantrr eleq2 eluni2 sylibr ex sylbid
      an4s ssrdv eqeq1d cin blssp inss1 sseq1 syl5ibrcom sylbird sstr expcom
      sylsyld visset eqeq1 anbi2d exbidv elab bitri cuz fzssuz uzssz sstri
      zssre fss mpan2 simprr opreq1 eqeq2d 19.23adv syl5bi r19.21aiv jctird
      cpw elnnuz fzfi sylbi cn0 simplr cle rpge0 flge0nn0 nn0p1nn ax1cn
      negnegi 1z znegcl ax-mp 0re 1re lt01 ltleii le0neg2 mpbi renegcli letri
      mp2an eluz mpbiri uzss sseli uzneg syl5eqelr sseldd xpfi pwfi mapval cvv
      mapsspw ssfi firnfi4 adantllr adantlrl jctild syl6 r19.21adva r19.23advv
      syl5 expdimp sylan2br pm2.61dan metres syl5eqel isbnd2 sstotbnd 3imtr4d
      impbid2 ) BSTZDCUAZUBZAUCTZAUDTZAAUEZUEZVVNUFZUGVVJVVNGUHZHUHZAUIUJUKZULZ
      HUMUNZGVVNUOZDIUHZUPZUAZJUHZKUHZLUHZBUQUJZUIUJZUKZULZKVWHUEUEZUNZJVWBUOZU
      BZIURUNZLUMUOZVVLVVKVVJVWAVVTGDUOZVWQVVJVVTGVVNDVVJVVNVWHDDUSUTZUEZUEZDVV
      JVVMVWTVVJAVWSAVWSULVVJFVAVBVBZVVJVWHVCTZDVWLUAZDVXAULVVHVXCVVIBVDZVEZVVH
      VXDVVIVVHVWLCDVVHVWLVFVGBVHUKZVIUKZCBVJZEVKVLVMZVWHVWLDVWLUFZVNVOZVPVQVVJ
      DVRULZVWRVWQVSZVVJVXMUBZVWQVWRVXOVWPLUMVXOVWPVWGUMTZVXOVRURTDVRUPZUAZVWMJ
      VRUOZUBZVWPVTVXOVXRVXSVXMVXRVVJVXMDVRVXQVXMWAVRVXQUAVXMVXQWBVAWCWDVWMJWEW
      FVWOVXTIVRURVWBVRULZVWDVXRVWNVXSVYAVWCVXQDVWBVRWGVLVWMJVWBVRWHWIWJWKVEWLW
      MVXMWNVVJDVRWOZVXNDVRWPVVJVYBVWRVWQVYBVWRUBVVTGDUNVVJVWQVVTGDWQVVJVVSVWQG
      HDUMVVJVVPDTZVVQUMTZUBZVVSVWQVSVVJVYEUBZVVSVWPLUMVYFVXPUBZVVSVXGVVQVWGBWR
      UJZWSUKZWSUKZWTUJZVGXAUKZXBZVYLVHUKZMUHZXCZNUHZOUHZVXGTZPUHZVYRVVPUJZVYIV
      YRVYOUJZXDUKZXAUKZULUBOPXEZVWGVWIUKZULZUBZMXFZNXGZURTZDWUJUPZUAZVWMJWUJUO
      ZUBZUBVWPVYGVVSWUOWUKVYGVVSWUMWUNVYGVVPVVQVWIUKZWULUAZVVSDWUPUAZWUMVYGKWU
      PWULVYFVVHVVPCTZVYDUBZUBZVXPVWFWUPTZVWFWULTZVSZVVHVVIVYEWVAVVIVYEUBWUTVVH
      VVIVYCVYDWUTVVIVYCUBZWUSVYDDCVVPXHZXIXJXKXLVVHVXPWUTWVDVVHWUSVXPVYDWVDVVH
      WUSUBZVXPVYDUBUBZWVBVWFVWLTZVVPVWFVWHUKVVQXMXNZUBZWVCWVGVYDWVBWVKXOZVXPWV
      GVXCVVPVWLTZUBVVQVFTXPVVQXMXNUBWVLVYDWVGVXCWVMVVHVXCWUSVXEVEVVHWUSWVMVVHC
      VWLVVPVVHVWLVXHCVXIEXQXRXSXTVVQYAVWFVWHVVPVVQVWLVXKYBYCYDWVHWVKWVCWVHWVKU
      BZVWFQUHZTZQWUJUNZWVCWVNVYSVYTWUAVYIVYRRUHZVXGTZVWBWVRVWFUJZWVRVVPUJZYEUK
      ZVYIWSUKZVGYFWSUKZXAUKZWTUJZULUBRIXEZUJZXDUKZXAUKZULUBOPXEZVWGVWIUKZWUJTV
      WFWWLTZWVQGKIPMORABCDHNLEFYGWVHWVIWWMWVJWVGVXPWVIWWMVYDWVGVXPUBZWVIUBWWNV
      WFVXHTZUBWWMWWNWVIWWOVVHWVIWWOVSWUSVXPVVHWVIWWOVVHVWLVXHVWFVXIXRZYHYIYJWV
      GVVHVVPVXHTZUBZVXPWWOWWMWUSWWQVVHWUSWWQCVXHVVPEYKZYLXKWWRVXPUBWWOWWMVVHVX
      PWWQWWOWWMVSVVHVXPUBZWWQWWOWWMWWTWWQWWOUBZUBZWWMWWKVWFVWHUKVWGXMXNZGKIPOR
      ABCDLEFYMWXBVXCWWKVWLTZWVIVWGVFTXPVWGXMXNUBZWWMWXCXOVVHVXCVXPWXAVXEYIWXBW
      XDVXGVFWWKXCZWXBWWJVFTZOVXGUOWXFWXBWXGOVXGWXBVYSUBZWUAVFTZWWIVFTZWXGWWTWW
      QVYSWXIWWOWWQVYSWXIWWTWWQVXGVFVVPXCZVYSWXIVFVXGVVPYNVGBVHYOZYPZVXGVFVYRVV
      PYQZYRYSYTWXHVYIVFTZWWHVFTZWXJWWTWXOWXAVYSVXPVVHWXOVXPVVHUBZVYIUMTZWXOVVH
      VXPVYHUMTZWXRVVHBUMTWXSBUUABUUBUUCVWGVYHUUDUUEZVYIUUFUUCUUGZYIWXBVXGVFWWG
      XCZVYSWXPWXBWWFVFTZRVXGUOWYBWXBWYCRVXGWXBWVSUBZWWEVFTZWWFUUHTWYCWYDWWCVFT
      ZWWDVFTWYEWYDWWBVFTZWXOVYIXPWOZWYFWXAWVSWYGWWTWXAWVSUBWVTVFTZWWAVFTZWYGWW
      OWVSWYIWWQWWOVXGVFVWFXCWVSWYIVFVXGVWFYNWXLYPVXGVFWVRVWFYQYRYSWWQWVSWYJWWO
      WWQWXKWVSWYJWXMVXGVFWVRVVPYQYRUUIWVTWWAUUJVOYSWWTWXOWXAWVSWYAYIWWTWYHWXAW
      VSVXPVVHWYHWXQWXRWYHWXTVYIUUKUUCUUGYIWWBVYIUULUUMYFUUNUUOUUPWWCWWDUUQUURW
      WEUUSWWFUUTUVAWLRIVXGVFWWFWWGWWGUFUVBUVCVXGVFVYRWWGYQUVDVYIWWHUVEVOWUAWWI
      UUQVOWLOPVXGVFWWJWWKWWKUFUVBUVCWXBWXDWWKVXHTWXFWXBVWLVXHWWKVVHVWLVXHULVXP
      WXAVXIYIXRVFVXGWWKYNWXLYPUVFUVGVVHWWOWVIVXPWWQVVHWVIWWOWWPVMUVHVXPWXEVVHW
      XAVWGYAUVIVWFVWHWWKVWGVWLVXKUVJUVKUVGUVLUVMUVNUVOUUCYTUVPWVPWWMQWWLWUJWVO
      WWLVWFUVQWJVOQVWFWUJUVRUVSUVTUWAUWBUVMUVDUWCVYFVVSWURVSVXPVYFVVSDVVRULZWU
      RVYFDVVNVVRVVJDVVNULVYEVVJDVXAVVNVXLVXBVPVEUWDVYFWURWYKVVRWUPUAZVVJVXCVXD
      UBZVYEWYLVVJVXCVXDVXFVXJXTWYMVYEUBZVVRWUPDUWEZWUPVVQDVWHAVWLVVPVXKFUWFWYO
      WUPUAWYNWUPDUWGVAWCUVDDVVRWUPUWHUWIUWJVEWURWUQWUMDWUPWULUWKUWLUWMVYGVWMJW
      UJVWEWUJTVYPVWEWUFULZUBZMXFZVYGVWMWUIWYRNVWEJUWNVYQVWEULZWUHWYQMWYSWUGWYP
      VYPVYQVWEWUFUWOUWPUWQUWRVYGWYQVWMMVYGWYQVWMVYGWYQUBWUEVWLTZWYPVWMVYGVYPWY
      TWYPVYGVYPUBZWYTVXGVFWUEXCZXUAWUDVFTZOVXGUOXUBXUAXUCOVXGVYGVYPVYSXUCVYGVY
      PVYSUBZUBWXIWUCVFTZXUCVYFVYSWXIVXPVYPVVJVYCVYSWXIVYDVVJVYCUBWXKVYSWXIVVHV
      VIVYCWXKWVEVVHWUSWXKWVFWUSWXKVVHWUSWXKWUSWWQWXKWWSWXMUWSYLWDUUEXLWXNUVDYT
      UVHVYGWXOWUBVFTZXUEXUDVVJVXPWXOVYEVVHVXPWXOVVIWYAUUIUUIVYPVXGVFVYOXCZVYSX
      UFVYPVYNVFUAXUGVYNUUHVFVYNVYMUWTUJZUUHVYMVYLUXAVYMUXBUXCUXDUXCVXGVYNVFVYO
      UXEUXFVXGVFVYRVYOYQUVDVYIWUBUVEYCWUAWUCUUQVOXLWLOPVXGVFWUDWUEWUEUFUVBUVCV
      YFWYTXUBXOZVXPVYPVVHXUIVVIVYEVVHWYTWUEVXHTXUBVVHVWLVXHWUEVXIXRVFVXGWUEYNW
      XLYPUVFYIYIUVGUVPVYGVYPWYPUXGVWKWYPKWUEVWLVWFWUEULVWJWUFVWEVWFWUEVWGVWIUX
      HUXIWJVOUVTUXJUXKUXLUXMVVJVYDVXPWUKVYCVVHVYDVXPWUKVVIVVHVYDUBVXPUBZVXGVYN
      USZUXNZURTZVYPMXGZURTWUKXUJXUKURTZXUMXUJVXGURTZVYNURTZXUOVVHXUPVYDVXPVVHB
      VGUWTUJZTXUPBUXOVGBUXPUXQYIXUJVYLSTZVYLXUHTXUQXUJVYJUMTZVYKUXRTZXUSXUJVYD
      WXRXUTVVHVYDVXPUXSVVHVXPWXRVYDVXPVVHWXRWXTUUGUUIVVQVYIUUDVOXUTVYJVFTXPVYJ
      UXTXNXVAVYJUUFVYJUYAVYJUYBVOVYKUYCUVAXUSXURXUHVYLXUSVGXUHTXURXUHUAXUSVGVG
      XBZXBZXUHVGUYDUYEXUSVYLXVBUWTUJZTZXVCXUHTXUSVYLXURTZXVEVYLUXOZXURXVDVYLVG
      XVDTZXURXVDUAXVBUUHTZVGUUHTZXVHXVJXVIUYFVGUYGUYHUYFXVIXVJUBXVHXVBVGUXTXNZ
      XVBXPUXTXNZXPVGUXTXNZXVKXVMXVLXPVGUYIUYJUYKUYLZVGVFTXVMXVLXOUYJVGUYMUYHUY
      NXVNXVBXPVGVGUYJUYOUYIUYJUYPUYQXVBVGUYRUYSUYQXVBVGUYTUYHVUAUXQXVBVYLVUBUU
      CVUCVYMVGUYTUUCXUSXVFXVGYLVUDVYMVYLUXPUVAVXGVYNVUEVOXUKVUFUVCXUMXUNVYNVXG
      VIUKZURVYNVXGMVYMVYLVHYOWXLVUGXUMXVOXULUAZXVOURTVXGVUHTXVPWXLVYNVXGVUHVUI
      UYHXULXVOVUJUXFVUCVYPNMWUFVUKUVAVULVUMVUNVWOWUOIWUJURVWBWUJULZVWDWUMVWNWU
      NXVQVWCWULDVWBWUJWGVLVWMJVWBWUJWHWIWJVUOVUPUVTVUQVURVUSVUTVVAUWAVVJVXCAVC
      TVVLVWAXOVXFVXCAVWSVCFVWHDVVBVVCGAVVNHVVOVVDUVAVVJVXCVXDVVKVWQXOVXFVXJKIV
      WHAVWLDJLVXKFVVEVOVVFVVG $.
      $( [2-Sep-2009] $)
  $}

  ${
    rrnheibor.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrnheibor.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    rrnheibor.3 $e |- T = ( MetOpen ` M ) $.
    rrnheibor.4 $e |- U = ( MetOpen ` ( Rn ` N ) ) $.
    $( Heine-Borel theorem for Euclidean space.  A subset of Euclidean space is
       compact iff it is closed and bounded. $)
    rrnheibor $p |- ( ( N e. NN /\ Y C_ X ) ->
                      ( T e. Comp <-> ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( cn wcel wss wa ccomp cms ctotbnd ccld cfv cbnd cme wb crrn rrnmet cxp
      cres metres syl5eqel syl adantr cdm eqid heibor eleq1i rrncms cr c1 cfz
      co cmap rrndm syl6reqr sseq2d biimpa cmsss syl11anc syl5bb rrntotbnd
      anbi12d bitrd ) DKLZFEMZNZAOLZCPLZCQLZNZFBRSLZCTLZNVMCUALZVNVQUBVKVTVLVKD
      UCSZUALZVTDUDWBCWAFFUEUFZUAHWAFUGUHUIUJACCUKUKZIWDULUMUIVMVOVRVPVSVOWCPLZ
      VMVRCWCPHUNVMWAPLZFWAUKUKZMZWEVRUBVKWFVLDUOUJVKVLWHVKEWGFVKWGUPUQDURUSUTU
      SEDVAGVBVCVDWABWGFWGULJVEVFVGCDEFGHVHVIVJ $.
      $( [2-Sep-2009] $)
  $}

