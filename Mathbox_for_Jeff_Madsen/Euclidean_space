$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Euclidean space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The real numbers are a complete metric space.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    recms $p |- ( ( abs o. - ) |` ( RR X. RR ) ) e. CMet $=
      ( vy vx cmin cr wcel cfv cc wss cc0 clt wbr co wa wb syl cle wceq c2 cexp
      adantr cabs ccom cxp cres cms copn ccld cdif cbl wrex wral cme eqid cnmet
      cv cnmetba isopn4 ax-mp difss cim eldifi imcl recn abscl 3syl wne sylancr
      0re ltlen absge0 wn eldifn reim0b necon3bbid mpbid abs00 necon3bid mpbird
      mpbir2and a1i blssm syl22anc sseld elbl cre caddc subcl syl2an recl sqge0
      resqcl addge02 absresq absvalsq2 imsub reim0 adantl opreq2d subid1 3eqtrd
      opreq1d eqtrd 3brtr4d le2sq cnmetdval breq2d metcl syl111anc lenlt bitr3d
      syl11anc ex con2d adantld sylbid eldif syl6ibr ssrdv breq2 opreq2 anbi12d
      jcad sseq1d rcla4ev syl12anc rgen mpbir2an opntop ax-resscn eqcomd iscld2
      ctop cuni uniopn2 mp2an mpbir cncms cmsss ) UACUBZDDUCUDUEEZDYSUFFZUGFEZU
      UBGDUHZUUAEZUUDUUCGHZIAUOZJKZBUOZUUFYSUIFZLZUUCHZMZADUJZBUUCUKZYSULEZUUDU
      UEUUNMNYSYSUMZUNZBAUUCYSUUAGYSUUPUPZUUAUMZUQURGDUSUUMBUUCUUHUUCEZUUHUTFZU
      AFZDEZIUVBJKZUUHUVBUUILZUUCHZUUMUUTUUHGEZUVADEZUVCUUHGDVAZUUHVBZUVHUVAGEZ
      UVCUVAVCZUVAVDOVEZUUTUVDIUVBPKZUVBIVFZUUTIDEUVCUVDUVNUVOMNVHUVMIUVBVIVGUU
      TUVKUVNUUTUVGUVHUVKUVIUVJUVLVEZUVAVJOZUUTUVOUVAIVFZUUTUUHDEZVKUVRUUHGDVLU
      UTUVSUVAIUUTUVGUVSUVAIQZNUVIUUHVMOVNVOUUTUVBIUVAIUUTUVKUVBIQUVTNUVPUVAVPO
      VQVRVSZUUTAUVEUUCUUTUUFUVEEZUUFGEZUUFDEZVKZMUUFUUCEUUTUWBUWCUWEUUTUVEGUUF
      UUTUUOUVGUVCUVDUVEGHUUOUUTUUQVTZUVIUVMUWAYSUUHUVBGUURWAWBWCUUTUWBUWCUUHUU
      FYSLZUVBJKZMZUWEUUTUUOUVGUVCUVDUWBUWINUWFUVIUVMUWAUUFYSUUHUVBGUURWDWBUUTU
      WHUWEUWCUUTUWDUWHUUTUWDUWHVKZUUTUWDMZUVBUUHUUFCLZUAFZPKZUWJUWKUWNUVBRSLZU
      WMRSLZPKZUWKUVARSLZUWLWEFZRSLZUWRWFLZUWOUWPPUWKIUWTPKZUWRUXAPKZUWKUWLGEZU
      WSDEZUXBUUTUVGUWCUXDUWDUVIUUFVCZUUHUUFWGWHZUWLWIZUWSWJVEUWKUWRDEZUWTDEZUX
      BUXCNUWKUVHUXIUUTUVHUWDUUTUVGUVHUVIUVJOTZUVAWKOUWKUXDUXEUXJUXGUXHUWSWKVEU
      WRUWTWLXKVOUWKUVHUWOUWRQUXKUVAWMOUWKUWPUWTUWLUTFZRSLZWFLZUXAUWKUXDUWPUXNQ
      UXGUWLWNOUWKUXMUWRUWTWFUWKUXLUVARSUWKUXLUVAUUFUTFZCLZUVAICLZUVAUUTUVGUWCU
      XLUXPQUWDUVIUXFUUHUUFWOWHUWKUXOIUVACUWDUXOIQUUTUUFWPWQWRUWKUVKUXQUVAQUUTU
      VKUWDUVPTUVAWSOWTXAWRXBXCUWKUVCUVNUWMDEZIUWMPKZUWNUWQNUUTUVCUWDUVMTZUUTUV
      NUWDUVQTUWKUXDUXRUXGUWLVDOUWKUXDUXSUXGUWLVJOUVBUWMXDWBVRUWKUVBUWGPKZUWNUW
      JUWKUWGUWMUVBPUUTUVGUWCUWGUWMQUWDUVIUXFUUHUUFYSUUPXEWHXFUWKUVCUWGDEZUYAUW
      JNUXTUWKUUOUVGUWCUYBUUOUWKUUQVTUUTUVGUWDUVITUWDUWCUUTUXFWQUUHUUFYSGUURXGX
      HUVBUWGXIXKXJVOXLXMXNXOYBUUFGDXPXQXRUULUVDUVFMAUVBDUUFUVBQZUUGUVDUUKUVFUU
      FUVBIJXSUYCUUJUVEUUCUUFUVBUUHUUIXTYCYAYDYEYFYGUUAYLEZDGHZUUBUUDNUUOUYDUUQ
      YSUUAUUSYHURYIDUUAGUUOGUUAYMZQUUQUUOUYFGYSUUAGUURUUSYNYJURYKYOYPYSUEEUYEY
      TUUBNYSUUPYQYIYSUUAGDUURUUSYRYOYP $.
      $( [2-Sep-2009] $)
  $}

  $c Rn $.
  $( Extend class notation with the n-dimensional Euclidean space. $)
  crrn $a class Rn $.

  ${
    $d n r x y d k $.
    $( Define n-dimensional Euclidean space as a metric space with the standard
       Euclidean norm given by the quadratic mean. $)
    df-rrn $a |- Rn = { <. n , r >. | ( n e. NN /\ r = { <. <. x , y >. , d >.
          | ( ( x e. ( RR ^m ( 1 ... n ) ) /\ y e. ( RR ^m ( 1 ... n ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... n )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) } $.
  $}

  ${
    $d N n r x y d k $.
    $( The n-dimensional Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnval $p |- ( N e. NN -> ( Rn ` N ) = { <. <. x , y >. , d >. |
            ( ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) /\
                      d = ( sqr ` sum_ k e. ( 1 ... N )
                                ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) } ) $=
      ( vn vr cv cr c1 cfz co cmap wcel wa cfv csu csqr wceq copab2 cmin opreq2
      c2 cexp crrn opreq2d eleq2d anbi12d sumeq1d fveq2d eqeq2d oprabbidv oprex
      cn df-rrn eqid oprabex2 fvopab4 ) FGDAHZIJFHZKLZMLZNZBHZVBNZOZEHZVACHZUSP
      VHVDPUALUCUDLZCQZRPZSZOZABETUSIJDKLZMLZNZVDVONZOZVGVNVICQZRPZSZOZABETZUNU
      EUTDSZVMWBABEWDVFVRVLWAWDVCVPVEVQWDVBVOUSWDVAVNIMUTDJKUBZUFZUGWDVBVOVDWFU
      GUHWDVKVTVGWDVJVSRWDVAVNVICWEUIUJUKUHULABCFGEUOABEVOVOVTWCIVNMUMZWGWCUPUQ
      UR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.  $d X x y d k $.  $d Y x y d k $.
    $( The value of the Euclidean metric.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnmval $p |- ( ( N e. NN /\ X e. ( RR ^m ( 1 ... N ) )
                  /\ Y e. ( RR ^m ( 1 ... N ) ) ) -> ( X ( Rn ` N ) Y ) =
        ( sqr ` sum_ k e. ( 1 ... N ) ( ( ( X ` k ) - ( Y ` k ) ) ^ 2 ) ) ) $=
      ( vx vy vd wcel co cfv cv cmin c2 cexp csu csqr wceq wa fveq1 opreq1d cfz
      cn cmap crrn copab2 rrnval opreqd fvex sumeq2sdv fveq2d opreq2d oprabval2
      cr c1 eqid sylan9eq 3impb ) BUBHZCUMUNBUAIZUCIZHZDUTHZCDBUDJZIZUSAKZCJZVE
      DJZLIZMNIZAOZPJZQURVAVBRVDCDEKZUTHFKZUTHRGKUSVEVLJZVEVMJZLIZMNIZAOZPJZQRE
      FGUEZIVKURVCVTCDEFABGUFUGEFGCDUTUTVSVKVTUSVFVOLIZMNIZAOZPJVJPUHVLCQZVRWCP
      WDUSVQWBAWDVPWAMNWDVNVFVOLVEVLCSTTUIUJVMDQZWCVJPWEUSWBVIAWEWAVHMNWEVOVGVF
      LVEVMDSUKTUIUJVTUOULUPUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.
    $( The underlying set of Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrndm $p |- ( N e. NN -> dom dom ( Rn ` N ) = ( RR ^m ( 1 ... N ) ) ) $=
      ( vx vy vd vk wcel cfv cdm cr c1 cfz co cv wa csqr wceq dmeqd c0 wne wn
      cn crrn cmap cxp cmin c2 cexp csu copab2 rrnval fvex eqid dmoprab2 syl6eq
      cc0 0re n0i ax-mp reex oprex map0 simplbi mto df-ne mpbir a1i dmxp eqtrd
      syl ) AUAFZAUBGZHZHIJAKLZUCLZVNUDZHZVNVJVLVOVJVLBMZVNFCMZVNFNDMVMEMZVQGVS
      VRGUELUFUGLEUHZOGZPNBCDUIZHVOVJVKWBBCEADUJQBCDVNVNWAWBVTOUKWBULUMUNQVJVNR
      SZVPVNPWCVJWCVNRPZTWDIRPZUOIFWETUPIUOUQURWDWEVMRSIVMUSJAKUTVAVBVCVNRVDVEV
      FVNVNVGVIVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k z m n $.
    $( Euclidean space is a metric space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnmet $p |- ( N e. NN -> ( Rn ` N ) e. Met ) $=
      ( vx vy vk vm wcel cfv cr wf co cc0 wceq caddc wral wa cmin cexp syl11anc
      c2 csqr vz vd vn cn crrn cdm cxp cv wb cle wbr cme c1 cfz cmap csu copab2
      w3a elnnuz biimpi 3ad2ant1 oprex elmap ffvelrn sylanb 3ad2antl2 3ad2antl3
      cuz reex resubcl resqcl syl r19.21aiva fsumrecl sqge0 jca fsumcmp0 3expib
      resqrcl r19.21aivv foprab2 sylib rrnval feq1d mpbird rrndm xpeq12d eleq2d
      eqid feq2d anbi12d rrnmval eqeq1d sqr00 fsum00 cc recn sqeq0 recnd subeq0
      bitrd ralbidva wfn ffn sylbi eqidd biantrurd eqfnfv bitr4d syl2an 3adant1
      3syl 3bitrd wi biimpd 3anbi123i copab simpl 3ad2antl1 fopab2 adantl trirn
      fveq2 opreq12d fvopab4 npncan adantll eqtr2d opreq1d sumeq2dv fveq2d cneg
      syl111anc negsubdi2 subcl eqcomd 3brtr4d 3expb ancom2s cvv sqneg 3adantr2
      3eqtr3d sylan2b 3adant3r3 3adantr1 3exp2 3imp syld r19.21aiv sylbid ismet
      fvex ax-mp sylanbrc ) AUDFZAUEGZUFUFZUURUGZHUUQIZBUHZCUHZUUQJZKLZUVAUVBLZ
      UIZUVCUAUHZUVAUUQJZUVGUVBUUQJZMJZUJUKZUAUURNZOZCUURNBUURNZUUQULFZUUPUUTHU
      MAUNJZUOJZUVQUGZHUUQIZUUPUVSUVRHUVAUVQFZUVBUVQFZOZUBUHUVPDUHZUVAGZUWCUVBG
      ZPJZSQJZDUPZTGZLOBCUBUQZIZUUPUWIHFZCUVQNBUVQNUWKUUPUWLBCUVQUVQUUPUVTUWAUW
      LUUPUVTUWAURZUWHHFZKUWHUJUKZUWLUWMAUMVHGFZUWGHFZDUVPNUWNUUPUVTUWPUWAUUPUW
      PAUSUTVAZUWMUWQDUVPUWMUWCUVPFZOZUWFHFZUWQUWTUWDHFZUWEHFZUXAUVTUUPUWSUXBUW
      AUVTUVPHUVAIZUWSUXBHUVPUVAVIUMAUNVBZVCZUVPHUWCUVAVDZVEVFZUWAUUPUWSUXCUVTU
      WAUVPHUVBIZUWSUXCHUVPUVBVIUXEVCZUVPHUWCUVBVDZVEVGZUWDUWEVJRZUWFVKVLZVMUWG
      DUMAVNRZUWMUWPUWQKUWGUJUKZOZDUVPNZUWOUWRUWMUXQDUVPUWTUWQUXPUXNUWTUXAUXPUX
      MUWFVOVLVPVMZUWGDUMAVQRZUWHVSRVRVTBCUBUVQUVQUWIHUWJUWJWIWAWBUUPUVRHUUQUWJ
      BCDAUBWCWDWEUUPUUSUVRHUUQUUPUURUVQUURUVQAWFZUYAWGWJWEUUPUVMBCUURUURUUPUVA
      UURFZUVBUURFZOUWBUVMUUPUYBUVTUYCUWAUUPUURUVQUVAUYAWHUUPUURUVQUVBUYAWHWKUU
      PUVTUWAUVMUWMUVFUVLUWMUVDUWIKLZUWHKLZUVEUWMUVCUWIKDAUVAUVBWLZWMUWMUWNUWOU
      YDUYEUIUXOUXTUWHWNRUWMUYEUWGKLZDUVPNZUWDUWELZDUVPNZUVEUWMUWPUXRUYEUYHUIUW
      RUXSUWGDUMAWORUWMUYGUYIDUVPUWTUYGUWFKLZUYIUWTUXAUWFWPFUYGUYKUIUXMUWFWQUWF
      WRXLUWTUWDWPFZUWEWPFZUYKUYIUIUWTUWDUXHWSUWTUWEUXLWSUWDUWEWTRXAXBUVTUWAUYJ
      UVEUIZUUPUVTUVAUVPXCZUVBUVPXCZUYNUWAUVTUXDUYOUXFUVPHUVAXDXEUWAUXIUYPUXJUV
      PHUVBXDXEUYOUYPOZUYJUVPUVPLZUYJOUVEUYQUYRUYJUYQUVPXFXGDUVPUVPUVAUVBXHXIXJ
      XKXMXMUWMUVKUAUURUWMUVGUURFZUVGUVQFZUVKUUPUVTUYSUYTXNUWAUUPUYSUYTUUPUURUV
      QUVGUYAWHXOVAUUPUVTUWAUYTUVKXNUUPUVTUWAUYTUVKUUPUVTUWAUYTURZOZUWIUVPUWCUV
      GGZUWDPJZSQJZDUPZTGZUVPVUCUWEPJZSQJZDUPZTGZMJZUVCUVJUJVUAUUPUXDUXIUVPHUVG
      IZURZUWIVULUJUKUVTUXDUWAUXIUYTVUMUXFUXJHUVPUVGVIUXEVCXPUUPVUNOZUVPUWCEUHZ
      UVPFZUCUHZVUPUVAGZVUPUVGGZPJZLOEUCXQZGZUWCVUQVURVUTVUPUVBGZPJZLOEUCXQZGZM
      JZSQJZDUPZTGZUVPVVCSQJZDUPZTGZUVPVVGSQJZDUPZTGZMJZUWIVULUJVUOUUPUVPHVVBIZ
      UVPHVVFIZVVKVVRUJUKUUPVUNXRVUNVVSUUPVUNVVAHFZEUVPNVVSVUNVWAEUVPVUNVUQOZVU
      SHFZVUTHFZVWAUXDUXIVUQVWCVUMUVPHVUPUVAVDXSVUMUXDVUQVWDUXIUVPHVUPUVGVDVGZV
      USVUTVJRVMEUCUVPHVVAVVBVVBWIZXTWBYAVUNVVTUUPVUNVVEHFZEUVPNVVTVUNVWGEUVPVW
      BVWDVVDHFZVWGVWEUXIUXDVUQVWHVUMUVPHVUPUVBVDVFVUTVVDVJRVMEUCUVPHVVEVVFVVFW
      IZXTWBYADAVVBVVFYBYMVUOUWHVVJTVUOUVPUWGVVIDVUOUWSOZUWFVVHSQVWJVVHUWDVUCPJ
      ZVUHMJZUWFUWSVVHVWLLVUOUWSVVCVWKVVGVUHMEUCUWCVVAVWKUVPVVBVUPUWCLZVUSUWDVU
      TVUCPVUPUWCUVAYCVUPUWCUVGYCZYDVWFUWDVUCPVBYEZEUCUWCVVEVUHUVPVVFVWMVUTVUCV
      VDUWEPVWNVUPUWCUVBYCYDVWIVUCUWEPVBYEZYDYAVUNUWSVWLUWFLZUUPVUNUWSOZUYLVUCW
      PFZUYMVWQVWRUWDUXDUXIUWSUXBVUMUXGXSWSZVWRVUCVUMUXDUWSVUCHFUXIUVPHUWCUVGVD
      VGWSZVWRUWEUXIUXDUWSUXCVUMUXKVFWSUWDVUCUWEYFYMYGYHYIYJYKVUOVUGVVNVUKVVQMV
      UOVUFVVMTVUOUVPVUEVVLDVUNUWSVUEVVLLUUPVWRVUDYLZSQJZVWKSQJVUEVVLVWRVXBVWKS
      QVWRVWSUYLVXBVWKLVXAVWTVUCUWDYNRYIVWRVUDWPFZVXCVUELVWRVWSUYLVXDVXAVWTVUCU
      WDYORVUDUUAVLVWRVWKVVCSQUWSVWKVVCLVUNUWSVVCVWKVWOYPYAYIUUCYGYJYKVUOVUJVVP
      TVUOUVPVUIVVODVWJVUHVVGSQUWSVUHVVGLVUOUWSVVGVUHVWPYPYAYIYJYKYDYQUUDUUPUVT
      UWAUVCUWILUYTUYFUUEVUBUVHVUGUVIVUKMUUPUVTUYTUVHVUGLZUWAUUPUYTUVTVXEUUPUYT
      UVTVXEDAUVGUVAWLYRYSUUBUUPUWAUYTUVIVUKLZUVTUUPUYTUWAVXFUUPUYTUWAVXFDAUVGU
      VBWLYRYSUUFYDYQUUGUUHUUIUUJVPVRUUKVTUUQYTFUVOUUTUVNOUIAUEUUMBCUAYTUUQUURU
      URWIUULUUNUUO $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M k n $.  $d X k n $.  $d N k n $.  $d A k $.  $d F k n $.  $d G k n $.
    $d R k n $.
    rrndstprj1.1 $e |- M = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    rrndstprj1.2 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    $( The distance between two points in Euclidean space is greater than the
       distance between the projections onto one coordinate.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj1 $p |- ( ( ( N e. NN /\ A e. ( 1 ... N ) ) /\ ( F e. X /\ G e. X
                    ) ) -> ( ( F ` A ) M ( G ` A ) ) <_ ( F ( Rn ` N ) G ) ) $=
      ( vk wcel c1 co wa cfv c2 cexp cle wbr cr syl11anc cfz cmin cabs csu csqr
      cn cv crrn cuz cc0 wral elfzuz2 adantr cmap eleq2i reex oprex elmap bitri
      wf ffvelrn sylanb adantlr adantll resubcl resqcl syl sqge0 jca r19.21aiva
      simplr fveq2 opreq12d opreq1d fsumlt1 syl111anc ancoms ad2ant2lr ad2ant2l
      wceq sylan2b absresq fsumrecl fsumcmp0 sqsqr 3brtr4d wb recn abscl absge0
      cc 3syl resqrcl sqrge0 syl22anc mpbird remetdval simpll ad2antrl ad2antll
      le2sq biimpi rrnmval ) EUFJZAKEUALZJZMZBFJZCFJZMZMZABNZACNZUBLZUCNZXEIUGZ
      BNZXPCNZUBLZOPLZIUDZUENZXLXMDLZBCEUHNLZQXKXOYBQRZXOOPLZYBOPLZQRZXKXNOPLZY
      AYFYGQXKEKUINJZXTSJZUJXTQRZMZIXEUKZXFYIYAQRXGYJXJUFAKEULUMZXKYMIXEXKXPXEJ
      ZMZYKYLYQXSSJZYKYQXQSJZXRSJZYRXJYPYSXGXHYPYSXIXHXESBUTZYPYSXHBSXEUNLZJZUU
      AFUUBBHUOZSXEBUPKEUAUQZURUSZXESXPBVAVBVCVDXJYPYTXGXIYPYTXHXIXESCUTZYPYTXI
      CUUBJZUUGFUUBCHUOZSXECUPUUEURUSZXESXPCVAVBVDVDXQXRVETZXSVFVGZYQYRYLUUKXSV
      HVGVIVJZXDXFXJVKXTYIIAKEXPAVTZXSXNOPUUNXQXLXRXMUBXPABVLXPACVLVMVNVOVPXKXN
      SJZYFYIVTXKXLSJZXMSJZUUOXFXHUUPXDXIXHXFUUAUUPUUFUUAXFUUPXESABVAVQWAVRZXFX
      IUUQXDXHXIXFUUGUUQUUJUUGXFUUQXESACVAVQWAVSZXLXMVETZXNWBVGXKYASJZUJYAQRZYG
      YAVTXKYJYKIXEUKUVAYOXKYKIXEUULVJXTIKEWCTZXKYJYNUVBYOUUMXTIKEWDTZYAWETWFXK
      XOSJZUJXOQRZYBSJZUJYBQRZYEYHWGXKUUOXNWKJZUVEUUTXNWHZXNWIWLXKUUOUVIUVFUUTU
      VJXNWJWLXKUVAUVBUVGUVCUVDYAWMTXKUVAUVBUVHUVCUVDYAWNTXOYBXAWOWPXKUUPUUQYCX
      OVTUURUUSXLXMDGWQTXKXDUUCUUHYDYBVTXDXFXJWRXHUUCXGXIXHUUCUUDXBWSXIUUHXGXHX
      IUUHUUIXBWTIEBCXCVPWF $.
      $( [2-Sep-2009] $)

    $( Bound on the distance between two points in Euclidean space given bounds
       on the distances in each coordinate.  This theorem and ~ rrndstprj1 can
       be used to show that the supremum norm and Euclidean norm are
       equivalent.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj2 $p |- ( ( ( N e. NN /\ F e. X /\ G e. X ) /\ ( R e. RR+ /\
                    A. n e. ( 1 ... N ) ( ( F ` n ) M ( G ` n ) ) < R ) ) ->
                                ( F ( Rn ` N ) G ) < ( R x. ( sqr ` N ) ) ) $=
      ( vk wcel cr c1 co clt wbr wa c2 wceq syl11anc cn w3a cfz cmap crp cv cfv
      wral crrn csqr cmul biid eleq2i 3anbi123i cmin cexp rrnmval adantr elnnuz
      csu cuz biimpi 3ad2ant1 wf oprex elmap ffvelrn sylanb 3ad2antl2 3ad2antl3
      reex resubcl resqcl adantlr rpre ad2antlr wi fveq2 opreq12d breq1d rcla4v
      syl imdistani ancoms cabs remetdval opreq1d absresq eqtrd ad2ant2r cc0 wb
      cle cme remet remetba metcl syl111anc metge0 rpge0 syl22anc biimpa anasss
      a1i lt2sq eqbrtrrd sylan2 expr imp 3jca r19.21aiva fsumlt caddc rpcn sqcl
      cc fsumconst syl2an nncn npcan sylancl 3ad2antl1 adantrr breqtrd fsumrecl
      ax-1cn sqge0 jca fsumcmp0 sqsqr nnre cn0 nnnn0 nn0ge0 resqrcl recnd sqmul
      mulcom 3eqtr3d sqrge0 3brtr4d remulcl mulge0 mpbird eqbrtrd ) FUAKZCGKZDG
      KZUBUUFCLMFUCNZUDNZKZDUUJKZUBZAUEKZBUFZCUGZUUODUGZENZAOPZBUUIUHZQZCDFUIUG
      NZAFUJUGZUKNZOPUUFUUFUUGUUKUUHUULUUFULGUUJCIUMGUUJDIUMUNUUMUVAQZUVBUUIJUF
      ZCUGZUVFDUGZUONZRUPNZJUTZUJUGZUVDOUUMUVBUVLSUVAJFCDUQURUVEUVLUVDOPZUVLRUP
      NZUVDRUPNZOPZUVEUVKFARUPNZUKNZUVNUVOOUVEUVKUUIUVQJUTZUVROUVEFMVAUGKZUVJLK
      ZUVQLKZUVJUVQOPZUBZJUUIUHUVKUVSOPUUMUVTUVAUUFUUKUVTUULUUFUVTFUSVBZVCZURUV
      EUWDJUUIUVEUVFUUIKZQUWAUWBUWCUUMUWGUWAUVAUUMUWGQZUVILKZUWAUWHUVGLKZUVHLKZ
      UWIUUKUUFUWGUWJUULUUKUUILCVDUWGUWJLUUICVKMFUCVEZVFUUILUVFCVGVHVIZUULUUFUW
      GUWKUUKUULUUILDVDUWGUWKLUUIDVKUWLVFUUILUVFDVGVHVJZUVGUVHVLTZUVIVMWBZVNUVA
      UWBUUMUWGUUNUWBUUTUUNALKZUWBAVOZAVMWBURVPUVEUWGUWCUUMUUNUUTUWGUWCVQUUMUUN
      QZUUTUWGUWCUUTUWGQUWSUWGUVGUVHENZAOPZQZUWCUWGUUTUXBUWGUUTUXAUUSUXABUVFUUI
      UUOUVFSZUURUWTAOUXCUUPUVGUUQUVHEUUOUVFCVRUUOUVFDVRVSVTWAWCWDUWSUXBQUWTRUP
      NZUVJUVQOUUMUWGUXDUVJSUUNUXAUWHUXDUVIWEUGZRUPNZUVJUWHUWTUXERUPUWHUWJUWKUW
      TUXESUWMUWNUVGUVHEHWFTWGUWHUWIUXFUVJSUWOUVIWHWBWIWJUWSUWGUXAUXDUVQOPZUWSU
      WGQZUXAUXGUXHUWTLKZWKUWTWMPZUWQWKAWMPZUXAUXGWLUXHEWNKZUWJUWKUXIUXLUXHEHWO
      XDZUUMUWGUWJUUNUWMVNZUUMUWGUWKUUNUWNVNZUVGUVHELEHWPZWQWRUXHUXLUWJUWKUXJUX
      MUXNUXOUVGUVHELUXPWSWRUUNUWQUUMUWGUWRVPUUNUXKUUMUWGAWTZVPUWTAXEXAXBXCXFXG
      XHXCXIXJXKUVJUVQJMFXLTUUMUUNUVSUVRSZUUTUUFUUKUUNUXRUULUUFUUNQZUVSFMUONMXM
      NZUVQUKNZUVRUUFUVTUVQXPKZUVSUYASUUNUWEUUNAXPKZUYBAXNZAXOWBUVQJMFXQXRUXSUX
      TFUVQUKUUFUXTFSZUUNUUFFXPKMXPKUYEFXSYFFMXTYAURWGWIYBYCYDUUMUVNUVKSZUVAUUM
      UVKLKZWKUVKWMPZUYFUUMUVTUWAJUUIUHUYGUWFUUMUWAJUUIUWPXKUVJJMFYETZUUMUVTUWA
      WKUVJWMPZQZJUUIUHUYHUWFUUMUYKJUUIUWHUWAUYJUWPUWHUWIUYJUWOUVIYGWBYHXKUVJJM
      FYITZUVKYJTURUUMUUNUVOUVRSZUUTUUFUUKUUNUYMUULUXSUVCAUKNZRUPNZUVCRUPNZUVQU
      KNZUVOUVRUUFUVCXPKZUYCUYOUYQSUUNUUFUVCUUFFLKZWKFWMPZUVCLKZFYKZUUFFYLKUYTF
      YMFYNWBZFYOTZYPZUYDUVCAYQXRUXSUYNUVDRUPUUFUYRUYCUYNUVDSUUNVUEUYDUVCAYRXRW
      GUXSUYPFUVQUKUUFUYPFSZUUNUUFUYSUYTVUFVUBVUCFYJTURWGYSYBYCUUAUUMUUNUVMUVPW
      LZUUTUWSUVLLKZWKUVLWMPZUVDLKZWKUVDWMPZVUGUUMVUHUUNUUMUYGUYHVUHUYIUYLUVKYO
      TURUUMVUIUUNUUMUYGUYHVUIUYIUYLUVKYTTURUUFUUKUUNVUJUULUUNUUFVUJUUNUWQVUAVU
      JUUFUWRVUDAUVCUUBXRWDYBUUFUUKUUNVUKUULUUNUUFVUKUUNUWQUXKQVUAWKUVCWMPZQVUK
      UUFUUNUWQUXKUWRUXQYHUUFVUAVULVUDUUFUYSUYTVULVUBVUCFYTTYHAUVCUUCXRWDYBUVLU
      VDXEXAYCUUDUUEVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d N f g j k m n r s x y z $.
    $( Euclidean space is complete.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrncms $p |- ( N e. NN -> ( Rn ` N ) e. CMet ) $=
      ( vn vj vk vr cn wcel cfv cv wbr wrex wi wral wa co wceq clt crp adantlr
      cr vf vx vm vz vy vg vs crrn cme cdm wf clm cca cms rrnmet wex copab cabs
      cfz cmin ccom cxp cres cle ffvelrn adantrr adantrl jca adantll cmap rrndm
      adantr eleq2d anbi12d biimpd imdistani eqid rrndstprj1 syl adantlrl oprex
      c1 reex elmap syl6bb expcom sylan9 imp remet remetba metcl syl11anc 3expb
      mp3an1 sylan rpre lelttr syl111anc mpand syldan anassrs fveq2 fveq1d fvex
      ad2antlr fvopab4 adantl opreq12d breq1d sylibrd imim2d ralimdva wb simprl
      reximdva iscau5 syl6rbb biimpar an32s sylan2 anasss ancom2s sylib sylancr
      r19.21aiva fopab2 3imtr4d breq1 breq2 ad2antrr adantllr lmbrnns ad3antrrr
      eqcomi a1i wne 3syl ex cc cc0 rexeqbidv recms mpbi simpri vtoclri biimprd
      iscms ac6s csqr cdiv biimpa eqcomd nnrp rpsqrcl rpdivcl ancoms rexralbidv
      imbi2d rcla4v sylbid cfn c0 wss cuz elnnuz fzfi sylbi biimpi eluzfz1 ne0i
      filbcmb w3a cmul 3ad2ant1 rrndstprj2 exp32 syld imp32 rpcn nnre cn0 nnnn0
      nnssre nn0ge0 resqrcl recn divcan1 3ad2antl1 breqtrd expdimp an4s reximdv
      rpne0 3syld r19.21adva eqidd syl3an1 3expa imdistand syland eximdv df-rex
      mpd sylibr iscms2 sylanbrc ) AFGZAUHHZUIGZFUXHUJUJZUAIZUKZUXKUBIZUXHULHJZ
      UBUXJKZLZUAUXHUMHZMUXHUNGAUOZUXGUXPUAUXQUXGUXKUXQGZNZUXLUXOUXTUXLNZUXMUXJ
      GZUXNNZUBUPZUXOUYAWBAUSOZTUXMUKZUCIZFGZUDIBIZUYGUXKHZHZPNUCUDUQZUYIUXMHZU
      RUTVATTVBVCZULHZJZBUYEMZNZUBUPZUYDUYAUYLUEIZUYOJZUETKZBUYEMUYSUYAVUBBUYEU
      YAUYIUYEGZNUYLUYNUMHZGZVUBUXTUXLVUCVUEUXGUXLVUCNZUXSVUEUXGVUFNZUXSVUEVUGC
      IZDIZVDJZVUHUXKHZVUIUXKHZUXHOZEIZQJZLZDFMZCFKZERMZVUJVUHUYLHZVUIUYLHZUYNO
      ZVUNQJZLZDFMZCFKZERMZUXSVUEVUGVURVVFERVUGVUNRGZNZVUQVVECFVVIVUHFGZNZVUPVV
      DDFVVKVUIFGZNZVUOVVCVUJVVMVUOUYIVUKHZUYIVULHZUYNOZVUNQJZVVCVVIVVJVVLVUOVV
      QLZVVIVVJVVLNZVUKUXJGZVULUXJGZNZVVRVUGVVSVWBVVHVUFVVSVWBUXGUXLVVSVWBVUCUX
      LVVSNVVTVWAUXLVVJVVTVVLFUXJVUHUXKVEVFUXLVVLVWAVVJFUXJVUIUXKVEZVGVHSVISVVI
      VWBNZVVPVUMVDJZVUOVVQVUGVWBVWEVVHUXGVUCVWBVWEUXLUXGVUCNZVWBNZVWFVUKTUYEVJ
      OZGZVULVWHGZNZNVWEVWFVWBVWKVWFVWBVWKVWFVVTVWIVWAVWJVWFUXJVWHVUKUXGUXJVWHP
      ZVUCAVKZVLZVMVWFUXJVWHVULVWNVMVNVOVPUYIVUKVULUYNAVWHUYNVQZVWHVQZVRVSVTSVW
      DVVPTGZVUMTGZVUNTGZVWEVUONVVQLVUGVWBVWQVVHUXGVUCVWBVWQUXLVWGVVNTGZVVOTGZV
      WQVWFVVTVWTVWAVWFVVTVWTUXGVVTUYETVUKUKZVUCVWTUXGVVTVXBUXGVVTVWIVXBUXGUXJV
      WHVUKVWMVMTUYEVUKWCWBAUSWAZWDWEVOVXBVUCVWTUYETUYIVUKVEWFWGWHVFVWFVWAVXAVV
      TVWFVWAVXAUXGVWAUYETVULUKZVUCVXAUXGVWAVXDUXGVWAVWJVXDUXGUXJVWHVULVWMVMZTU
      YEVULWCVXCWDWEVOVXDVUCVXAUYETUYIVULVEWFWGWHVGUYNUIGZVWTVXAVWQUYNVWOWIZVVN
      VVOUYNTUYNVWOWJZWKWNWLVTSVUGVWBVWRVVHUXGVWBVWRVUFUXGUXIVWBVWRUXRUXIVVTVWA
      VWRVUKVULUXHUXJUXJVQZWKWMWOSSVVHVWSVUGVWBVUNWPXEVVPVUMVUNWQWRWSWTXAVVMVVB
      VVPVUNQVVMVUTVVNVVAVVOUYNVVJVUTVVNPVVIVVLUCUDVUHUYKVVNFUYLUYGVUHPUYIUYJVU
      KUYGVUHUXKXBXCUYLVQZUYIVUKXDXFXEVVLVVAVVOPVVKUCUDVUIUYKVVOFUYLUYGVUIPUYIU
      YJVULUYGVUIUXKXBXCVXJUYIVULXDXFZXGXHXIXJXKXLXOXLVUGUXIUXLUXSVUSXMUXGUXIVU
      FUXRVLUXGUXLVUCXNEUXHCDUXKUXJVXIXPWLVUGVXFFTUYLUKZVUEVVGXMVXGVUGUYKTGZUCF
      MZVXLUXGVUCUXLVXNUXGVUCUXLVXNVWFUXLNVXMUCFVWFUXLUYHVXMUXLUYHNZVWFUYJUXJGZ
      VXMFUXJUYGUXKVEZUXGVXPVUCVXMUXGVXPNUYETUYJUKZVUCVXMUXGVXRVXPUXGVXPUYJVWHG
      VXRUXGUXJVWHUYJVWMVMTUYEUYJWCVXCWDXQXRZUYETUYIUYJVEZWOXSXTXAYEYAYBUCUDFTU
      YKUYLVXJYFZYCEUYNCDUYLTVXHXPYDYGWHXSXAUFIZUYTUYOJZUEUYNUJUJZKZVUBUFUYLVUD
      VYBUYLPZVYCVUAUEVYDTVYDTPVYFTVYDVXHYNYOVYBUYLUYTUYOYHUUAVXFVYEUFVUDMZUYNU
      NGVXFVYGNUUBUEUYNUFVYDVYDVQUUGUUCUUDUUEVSYEVUAUYPBUEUYETUBVXCUYTUYMUYLUYO
      YIUUHVSUYAUYRUYCUBUYAUYFUYBUYQUYCUYAUYBUYFUYAUYBUXMVWHGZUYFUYAUXJVWHUXMUX
      GVWLUXSUXLVWMYJVMTUYEUXMWCVXCWDZWEUUFUYAUYBUYQUXNUXGUXLUYBUYQUXNLZLUXSUXG
      UXLNZUYBVYJVYKUYBNZUYQVUJVULUXMUXHOZVUNQJZLZDFMZCFKZERMZUXNVYLUYQVYQERVYL
      VVHNZUYQVUJVVOUYMUYNOZVUNAUUIHZUUJOZQJZLZDFMCFKZBUYEMZVUJWUCBUYEMZLZDFMZC
      FKZVYQVYSUYPWUEBUYEVYSVUCNZUYPVUJVYTUGIZQJZLZDFMCFKZUGRMZWUEWUKVXFUYMTGZV
      XLUYPWUPXMVXFWUKVXGYOVYLVUCWUQVVHUXGUYBVUCWUQUXLUXGUYBNZUYFVUCWUQWURVYHUY
      FUXGUYBVYHUXGUXJVWHUXMVWMVMZUUKVYIYCUYETUYIUXMVEWOYKSWUKVXNVXLVYLVUCVXNVV
      HVYKVUCVXNUYBVYKVUCNVXMUCFVYKUYHVUCVXMVYKUYHNVXRVUCVXMUXGUXLUYHVXRVXOUXGV
      XPVXRVXQVXSXTXAVXTWOXSYESSVYAYCUGVVOUYNUYMCDUYLTVXHVVLVVAVVOVXKUULYLWRVYS
      WUPWUELZVUCVYKVVHWUTUYBUXGVVHWUTUXLUXGVVHNZWUBRGZWUTVVHUXGWVBUXGVVHWUARGZ
      WVBUXGARGZWVCAUUMZAUUNZVSVUNWUAUUOXTZUUPWUOWUEUGWUBRWULWUBPZWUNWUDCDFFWVH
      WUMWUCVUJWULWUBVYTQYIUURUUQUUSVSSSVLUUTXLVYSUYEUVAGZUYEUVBYPZFTUVCZWUFWUJ
      LUXGWVIUXLUYBVVHUXGAWBUVDHGZWVIAUVEZWBAUVFUVGYMUXGWVJUXLUYBVVHUXGWVLWBUYE
      GWVJUXGWVLWVMUVHWBAUVIUYEWBUVJYQYMWVKVYSUWCYOWUCBCDUYEFUVKWRVYSWUIVYPCFVY
      SWUHVYODFVYSVVLNZWUGVYNVUJWVNVYSVWANWUGVYNLZVYSVVLVWAVYKVVLVWALZUYBVVHUXL
      WVPUXGUXLVVLVWAVWCYRXGYJVPVYLVVHVWAWVOUXGUYBVVHVWANWVOUXLUXGVVHUYBVWAWVOU
      XGUYBVWANZVVHWVOUXGWVQNUXGVWJVYHNZNZVVHWVOUXGVWAUYBWVSUXGVWAUYBNZWVRUXGWV
      TWVRUXGVWAVWJUYBVYHVXEWUSVNVOVPYBWVSVVHWUGVYNUXGVWJVYHVVHWUGNZVYNLUXGVWJV
      YHUVLZWWAVYNWWBWWANVYMWUBWUAUVMOZVUNQWWBVVHWUGVYMWWCQJZWWBVVHWVBWUGWWDLUX
      GVWJVVHWVBLVYHVVHUXGWVBWVGWFUVNWWBWVBWUGWWDWUBBVULUXMUYNAVWHVWOVWPUVOUVPU
      VQUVRWWBVVHWWCVUNPZWUGUXGVWJVVHWWEVYHWVAVUNYSGZWUAYSGZWUAYTYPZWWEVVHWWFUX
      GVUNUVSXGUXGWWGVVHUXGATGZYTAVDJZNWUATGWWGUXGWWIWWJAUVTUXGAUWAGWWJAUWBAUWD
      VSVHAUWEWUAUWFYQVLUXGWWHVVHUXGWVDWVCWWHWVEWVFWUAUWMYQVLVUNWUAUWGWRUWHVFUW
      IYRWMUWJWOXSUWKYKXAVSXKXLUWLUWNUWOUXGUYBUXLUXNVYRXMZUXGUYBUXLWWKUXGUXIUYB
      UXLWWKUXREVULUXHUXMCDUXKUXJVXIVVLVULUWPYLUWQUWRXSXJYRSUWSUWTUXAUXCUXNUBUX
      JUXBUXDYRYEUBUXHUAUXJVXIUXEUXF $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X a b d f m n r u v w x y z $.  $d M a b d f m n r u v w x y z $.
    $d Y a b d f m n r u v w x y z $.  $d N a b d f m n r u v w x y z $.
    rrntotbnd.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrntotbnd.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem1 $p |- ( ( ( ( N e. NN /\ x e. X ) /\ ( d e. RR+ /\ r e. RR+ )
          ) /\ ( y e. dom dom ( Rn ` N ) /\ ( x ( Rn ` N ) y ) < r ) ) ->
        ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
            ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\
            v = ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( ball ` ( Rn ` N ) ) d ) e.
        { a | E. f ( f : ( 1 ... N ) --> ( -u ( ( |_ ` ( r / ( d / ( sqr ` N )
            ) ) ) + 1 ) ... ( ( |_ ` ( r / ( d / ( sqr ` N ) ) ) ) + 1 ) ) /\
                a = ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
                                ( ( d / ( sqr ` N ) ) x. ( f ` m ) ) ) ) }
                                            ( ball ` ( Rn ` N ) ) d ) ) } ) $=
      ( wcel wa co cr cn cv crp crrn cfv cdm clt wbr c1 cfz csqr cdiv cfl caddc
      cneg wf cmin c2 wceq copab cmul cbl wex cab wral cabs cle cn0 simplr nnrp
      cz rpsqrcl syl rpdivcl sylan2 ancoms adantlr syl11anc rpre rpge0 flge0nn0
      cc0 nn0p1nn 3syl an31s ad2antrr cmap rrndm eleq2d reex oprex elmap syl6bb
      biimpa ffvelrn sylan adantllr eleq2i bitri adantll resubcl ad2ant2r rpne0
      wne redivcl syl111anc 2re 2ne0 rereccli readdcl sylancl flcl adantlrr zcn
      sylanb cc abscl recnd peano2zdi zre anasss peano2re absrdbnd absdiv absid
      opreq2d eqtrd cme adantr a1i wi eqbrtrd letrd sylib mpbid 3ad2ant1 eqeq2d
      eqid wb anbi2d recn syl6reqr simpr metcl ad2antll ccom cxp cres remetdval
      rrnmet remet remetba metsym biimprd imdistani rrndstprj1 an32s ex anassrs
      imp eqbrtrrd adantl ad2antlr ltle impr elrp lediv1 flwordi 1re leadd1 w3a
      nnre absle syl2an biimp3a simp2 nnnegz nnz mpbird r19.21aiva fopab2 eqidd
      elfz opabex2 feq1 fveq1 opabbidv opreq1d anbi12d cla4ev eqeq1 exbidv elab
      sylibr ) IUAQZAUBZJQZRZNUBZUCQZLUBZUCQZRZRZBUBZIUDUEZUFUFZQZUWPUXEUXFSZUX
      AUGUHZRZRZUIIUJSZUXAUWSIUKUEZULSZULSZUMUEZUIUNSZUOZUXRUJSZEUBZUPZFUBZUXMQ
      ZDUBZUYCUWPUEZUXOUYCGUBZUXMQZCUBUYGUXEUEZUYGUWPUEZUQSZUXOULSZUIURULSZUNSZ
      UMUEZUSRGCUTZUEZVASZUNSZUSZRZFDUTZUWSUXFVBUEZSZUYDUYEUYFUXOUYCUYAUEZVASZU
      NSZUSZRZFDUTZUWSVUCSZUSZRZEVCZVUDUYBMUBZVUKUSZRZEVCZMVDQUXLUXMUXTUYPUPZVU
      DVUDUSZVUNUXLUYOUXTQZGUXMVEVUSUXLVVAGUXMUXLUYHRZUXRUAQZUYOVKQZUYOVFUEZUXR
      VGUHZVVAUXDVVCUXKUYHUWOUXCVVCUWQUXCUWOVVCUWOUXBUWTVVCUWOUXBRUWTRZUXPUCQZU
      XQVHQZVVCVVGUXBUXOUCQZVVHUWOUXBUWTVIUWOUWTVVJUXBUWTUWOVVJUWOUWTUXNUCQZVVJ
      UWOIUCQVVKIVJIVLVMUWSUXNVNVOZVPZVQUXAUXOVNZVRVVHUXPTQZWBUXPVGUHVVIUXPVSZU
      XPVTUXPWAVRUXQWCWDWEVPVQWFUXDUXHUYHVVDUXJUXDUXHRZUYHRZUYNTQZVVDVVRUYLTQZU
      YMTQVVSVVRUYKTQZUXOTQZUXOWBXDZVVTUWRUXHUYHVWAUXCUWRUXHRZUYHRZUYITQZUYJTQZ
      VWAUWOUXHUYHVWFUWQUWOUXHRUXMTUXEUPZUYHVWFUWOUXHVWHUWOUXHUXETUXMWGSZQVWHUW
      OUXGVWIUXEIWHZWITUXMUXEWJUIIUJWKZWLWMWNUXMTUYGUXEWOWPWQZUWRUYHVWGUXHUWQUY
      HVWGUWOUWQUXMTUWPUPZUYHVWGUWQUWPVWIQVWMJVWIUWPOWRTUXMUWPWJVWKWLWSUXMTUYGU
      WPWOXOWTVQZUYIUYJXAVRWQZUXDVWBUXHUYHUWOUWTVWBUWQUXBUWTUWOVWBUWTUWORZVVJVW
      BVVLUXOVSVMVPZXBWFUXDVWCUXHUYHUWOUWTVWCUWQUXBUWTUWOVWCVWPVVJVWCVVLUXOXCVM
      VPXBZWFUYKUXOXEXFZURXGXHXIUYLUYMXJXKZUYNXLZVMXMVVBVVEUYLVFUEZUMUEZUIUNSZU
      XRVVBUYOXPQZVVETQVVBVVSVVDVXEUXDUXHUYHVVSUXJVWTXMVXAUYOXNWDUYOXQVMVVBVXBT
      QZVXDVKQVXDTQUXDUXHUYHVXFUXJVVRUYLXPQVXFVVRUYLVWSXRUYLXQVMXMZVXFVXCVXBXLZ
      XSVXDXTWDVVBUXQTQZUXRTQZUXDVXIUXKUYHUWOUXCVXIUWQUWOUXCRVVOUXQVKQVXIUWOUWT
      UXBVVOUXBUWOUWTRZVVOUXBVXKRVVHVVOVXKUXBVVJVVHVVMVVNVOVVPVMVPYAZUXPXLUXQXT
      WDVQWFZUXQYBVMVVBVVTVVEVXDVGUHUXDUXHUYHVVTUXJVWSXMUYLYCVMVVBVXCUXQVGUHZVX
      DUXRVGUHZVVBVXFVVOVXBUXPVGUHVXNVXGUXDVVOUXKUYHUWOUXCVVOUWQVXLVQWFVVBVXBUY
      KVFUEZUXOULSZUXPVGVVBVXBVXPUXOVFUEZULSZVXQVVBUYKXPQZUXOXPQZVWCVXBVXSUSVVB
      UYKUXDUXHUYHVWAUXJVWOXMZXRUXDVYAUXKUYHUWOUWTVYAUWQUXBVXKUXOVWQXRXBWFUXDVW
      CUXKUYHVWRWFUYKUXOYDXFVVBVXRUXOVXPULUXDVXRUXOUSZUXKUYHUWOUWTVYCUWQUXBVXKV
      WBWBUXOVGUHZVYCVWQVXKVVJVYDVVMUXOVTVMUXOYEVRXBWFYFYGVVBVXPUXAVGUHZVXQUXPV
      GUHZVVBVXPUXIUXAVVBVWAVXTVXPTQZVYBUYKUUAUYKXQWDZUXLUXITQZUYHUWRUXHVYIUXCU
      XJVWDUXFYHQZUWPUXGQZUXHVYIUWOVYJUWQUXHIUUJWFUWRVYKUXHUWOUWQVYKUWOJUXGUWPU
      WOUXGVWIJVWJOUUBZWIWNYIUWRUXHUUCUWPUXEUXFUXGUXGYRUUDXFZXBYIUXDUXATQZUXKUY
      HUXBVYNUWRUWTUXAVSZUUEWFZUWRUXKUYHVXPUXIVGUHZUXCUWRUXHUYHVYQUXJVWEUYIUYJV
      FUQUUFTTUUGUUHZSZVXPUXIVGVWEVWFVWGVYSVXPUSVWLVWNUYIUYJVYRVYRYRZUUIVRVWEVY
      SUYJUYIVYRSZUXIVGVWEVYRYHQZVWFVWGVYSWUAUSWUBVWEVYRVYTUUKYJVWLVWNUYIUYJVYR
      TVYRVYTUULUUMXFVWDUWRUXEJQZRZUYHWUAUXIVGUHZUWRUXHWUCUWOUXHWUCYKUWQUWOWUCU
      XHUWOJUXGUXEVYLWIUUNYIUUOWUDUYHWUEUWOUWQWUCUYHWUEYKUWOUWQWUCRZRUYHWUEUWOU
      YHWUFWUEUYGUWPUXEVYRIJVYTOUUPUUQUURUUSUUTWPYLUVAXMWQUXLUXIUXAVGUHZUYHUXDU
      XHUXJWUGVVQVYIVYNUXJWUGYKUWRUXHVYIUXCVYMVQUXCVYNUWRUXHUXBVYNUWTVYOUVBUVCU
      XIUXAUVDVRUVEYIYMVVBVYGVYNVWBWBUXOUGUHRZVYEVYFYSVYHVYPVVBVVJWUHUXDVVJUXKU
      YHUWOUWTVVJUWQUXBVVMXBWFUXOUVFYNVXPUXAUXOUVGXFYOYLVXBUXPUVHXFVVBVXCTQZVXI
      UITQZVXNVXOYSVVBVXFVXCVKQWUIVXGVXHVXCXTWDVXMWUJVVBUVIYJVXCUXQUIUVJXFYOYMV
      VCVVDVVFUVKZVVAUXSUYOVGUHUYOUXRVGUHRZVVCVVDVVFWULVVDVVCVVFWULYSZVVDUYOTQV
      XJWUMVVCUYOXTUXRUVLUYOUXRUVMUVNVPUVOWUKVVDUXSVKQZUXRVKQZVVAWULYSVVCVVDVVF
      UVPVVCVVDWUNVVFUXRUVQYPVVCVVDWUOVVFUXRUVRYPUYOUXSUXRUWCXFUVSXFUVTGCUXMUXT
      UYOUYPUYPYRUWAYNUXLVUDUWBVUMVUSVUTREUYPGCUXMUYOVWKUWDUYAUYPUSZUYBVUSVULVU
      TUXMUXTUYAUYPUWEWUPVUKVUDVUDWUPVUJVUBUWSVUCWUPVUIVUAFDWUPVUHUYTUYDWUPVUGU
      YSUYEWUPVUFUYRUYFUNWUPVUEUYQUXOVAUYCUYAUYPUWFYFYFYQYTUWGUWHYQUWIUWJVRVURV
      UNMVUDVUBUWSVUCWKVUOVUDUSZVUQVUMEWUQVUPVULUYBVUOVUDVUKUWKYTUWLUWMUWN $.
      $( [2-Sep-2009] $)

    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem2 $p |- ( ( ( N e. NN /\ d e. RR+ ) /\
          ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) ) ->
                  ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
        ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\ v =
                ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                      ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( Rn ` N ) y ) < d ) $=
      ( wcel wa cr co cfv wceq cmul va cn cv crp c1 cfz cmap csqr cdiv c2 caddc
      cmin cfl copab crrn clt cabs ccom cxp cres wbr wral simpll wf oprex elmap
      reex ffvelrn sylanb adantll adantlrr nnrp rpsqrcl syl rpdivcl sylan2 rpre
      ancoms ad2antrr cz cc0 wne adantlr resubcl syl11anc redivcl syl111anc 2re
      rpne0 2ne0 rereccli readdcl sylancl flcl zre 3syl r19.21aiva fopab2 sylib
      sylan remulcl sylibr simprr adantr fveq2 opreq2d opreq12d fvopab4 opreq1d
      eqid adantl fveq2d fvex jca simprl remetdval cc recnd subcl abscl a1i 1re
      cle rddif halflt1 lelttrd wb rpgt0 ltmul2 syl112anc mpbid ad2antrl addcom
      recn ad2antll subsub3 rpcn divcan2 eqcomd eqbrtrd 3eqtr2d 3brtr4d anassrs
      subdi eqtr4d absmul rpge0 absid 3eqtrd mulid1 rrndstprj2 syl32anc divcan1
      syl2an breqtrd ) HUBNZKUCZUDNZOZAUCZPUEHUFQZUGQZNZBUCZUVBNZOZOZEUCZUVANZD
      UCUVHUUTRZUUQHUHRZUIQZUVHFUCZUVANZCUCUVMUVDRZUVMUUTRZULQZUVLUIQZUEUJUIQZU
      KQZUMRZSOFCUNZRZTQZUKQZSOEDUNZUVDHUORQZUVLUVKTQZUUQUPUVGUUPUWFUVBNZUVEUVL
      UDNZUAUCZUWFRZUWKUVDRZUQULURPPUSUTZQZUVLUPVAZUAUVAVBUWGUWHUPVAUUPUURUVFVC
      UVGUVAPUWFVDZUWIUVGUWEPNZEUVAVBUWQUVGUWREUVAUVGUVIOZUVJPNZUWDPNZUWRUUSUVC
      UVIUWTUVEUVCUVIUWTUUSUVCUVAPUUTVDZUVIUWTPUVAUUTVGUEHUFVEZVFZUVAPUVHUUTVHV
      IVJVKUWSUVLPNZUWCPNZUXAUUSUXEUVFUVIUURUUPUXEUURUUPOZUWJUXEUUPUURUVKUDNZUW
      JUUPHUDNZUXHHVLZHVMZVNUUQUVKVOVPZUVLVQZVNVRZVSUVGUVAPUWBVDZUVIUXFUVGUWAPN
      ZFUVAVBUXOUVGUXPFUVAUVGUVNOZUVTPNZUWAVTNUXPUXQUVRPNZUVSPNZUXRUXQUVQPNZUXE
      UVLWAWBZUXSUVFUVNUYAUUSUVFUVNOUVOPNZUVPPNZUYAUVEUVNUYCUVCUVEUVAPUVDVDZUVN
      UYCPUVAUVDVGUXCVFZUVAPUVMUVDVHVIVJUVCUVNUYDUVEUVCUXBUVNUYDUXDUVAPUVMUUTVH
      VIWCUVOUVPWDWEVJUUSUXEUVFUVNUXNVSUUSUYBUVFUVNUURUUPUYBUXGUWJUYBUXLUVLWIZV
      NVRVSUVQUVLWFWGUJWHWJWKZUVRUVSWLWMUVTWNUWAWOWPWQFCUVAPUWAUWBUWBXJZWRWSUVA
      PUVHUWBVHWTUVLUWCXAWEUVJUWDWLWEWQEDUVAPUWEUWFUWFXJZWRWSPUVAUWFVGUXCVFXBUU
      SUVCUVEXCUUSUWJUVFUURUUPUWJUXLVRZXDUVGUWPUAUVAUVGUWKUVANZOZUWOUWKUUTRZUVL
      UWKUWBRZTQZUKQZUWMUWNQZUVLUPUYLUWOUYRSUVGUYLUWLUYQUWMUWNEDUWKUWEUYQUVAUWF
      UVHUWKSZUVJUYNUWDUYPUKUVHUWKUUTXEUYSUWCUYOUVLTUVHUWKUWBXEXFXGUYJUYNUYPUKV
      EXHXIXKUYMUYRUYNUVLUWMUYNULQZUVLUIQZUVSUKQZUMRZTQZUKQZUWMUWNQZUVLUPUYLUYR
      VUFSUVGUYLUYQVUEUWMUWNUYLUYPVUDUYNUKUYLUYOVUCUVLTFCUWKUWAVUCUVAUWBUVMUWKS
      ZUVTVUBUMVUGUVRVUAUVSUKVUGUVQUYTUVLUIVUGUVOUWMUVPUYNULUVMUWKUVDXEUVMUWKUU
      TXEXGXIXIXLUYIVUBUMXMXHXFXFXIXKUUSUVFUYLVUFUVLUPVAZUUSUWJUYNPNZUWMPNZOZVU
      HUVFUYLOZUYKVULVUIVUJUVCUYLVUIUVEUVCUXBUYLVUIUXDUVAPUWKUUTVHVIWCUVEUYLVUJ
      UVCUVEUYEUYLVUJUYFUVAPUWKUVDVHVIVJXNUWJVUKOZVUFVUEUWMULQZUQRZUVLUPVUMVUEP
      NZVUJVUFVUOSVUMVUIVUDPNZVUPUWJVUIVUJXOVUMUXEVUCPNZVUQUWJUXEVUKUXMXDZVUMVU
      BPNZVUCVTNVURVUMVUAPNZUXTVUTVUMUYTPNZUXEUYBVVAVUKVVBUWJVUJVUIVVBUWMUYNWDV
      RXKZVUSUWJUYBVUKUYGXDZUYTUVLWFWGZUYHVUAUVSWLWMVUBWNVUCWOWPZUVLVUCXAWEZUYN
      VUDWLWEUWJVUIVUJXCVUEUWMUWNUWNXJZXPWEVUMUVLVUCVUAULQZUQRZTQZUVLUETQZVUOUV
      LUPVUMVVJUEUPVAZVVKVVLUPVAZVUMVVJUVSUEVUMVVIXQNZVVJPNZVUMVUCXQNZVUAXQNZVV
      OVUMVUCVVFXRZVUMVUAVVEXRZVUCVUAXSWEZVVIXTVNZUXTVUMUYHYAUEPNZVUMYBYAZVUMVV
      AVVJUVSYCVAVVEVUAYDVNUVSUEUPVAVUMYEYAYFVUMVVPVWCUXEWAUVLUPVAZVVMVVNYGVWBV
      WDVUSUWJVWEVUKUVLYHXDVVJUEUVLYIYJYKVUMVUOUVLVVITQZUQRZUVLUQRZVVJTQZVVKVUM
      VUNVWFUQVUMVUNVUDUYNUKQZUWMULQZVUDUYTULQZVWFVUMVUEVWJUWMULVUMUYNXQNZVUDXQ
      NZVUEVWJSVUIVWMUWJVUJUYNYNYLZVUMVUDVVGXRZUYNVUDYMWEXIVUMVWNUWMXQNZVWMVWLV
      WKSVWPVUJVWQUWJVUIUWMYNYOVWOVUDUWMUYNYPWGVUMVWLVUDUVLVUATQZULQZVWFVUMUYTV
      WRVUDULVUMVWRUYTVUMUYTXQNUVLXQNZUYBVWRUYTSVUMUYTVVCXRUWJVWTVUKUVLYQZXDZVV
      DUYTUVLYRWGYSXFVUMVWTVVQVVRVWFVWSSVXBVVSVVTUVLVUCVUAUUDWGUUEUUAXLVUMVWTVV
      OVWGVWISVXBVWAUVLVVIUUFWEVUMVWHUVLVVJTVUMUXEWAUVLYCVAZVWHUVLSVUSUWJVXCVUK
      UVLUUGXDUVLUUHWEXIUUIVUMVVLUVLUWJVVLUVLSZVUKUWJVWTVXDVXAUVLUUJVNXDYSUUBYT
      UUNUUCYTYTWQUVLUAUWFUVDUWNHUVBVVHUVBXJUUKUULUUSUWHUUQSZUVFUUSUUQXQNZUVKXQ
      NZUVKWAWBZVXEUURVXFUUPUUQYQXKUUPVXGUURUUPUXIUXHVXGUXJUXKUVKYQWPXDUUPVXHUU
      RUUPUXIUXHVXHUXJUXKUVKWIWPXDUUQUVKUUMWGXDUUO $.
      $( [2-Sep-2009] $)

    $( A set in Euclidean space is totally bounded iff its is bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrntotbnd $p |- ( ( N e. NN /\ Y C_ X ) ->
                                      ( M e. TotBnd <-> M e. Bnd ) ) $=
      ( vm wcel wss wa cv cfv co wceq crp cfn cr c1 syl11anc c0 vx vr vv vb cdm
      vy vd vf va vu vz vn eqid cbl wrex wral cuni cxp a1i dmeqd cme adantr cfz
      cn sseq2d biimpar eqtr4d wi eqsstrd adantl unieq raleq anbi12d r19.21aiva
      cmap rcla4ev wne cdiv cfl caddc cneg wf cmul copab wex cab anim2i anassrs
      clt wbr wb cc0 eleq2d jca rpregt0 syl2an ad2antrr biimpi reex oprex elmap
      c2 ffvelrn sylanb adantll adantlrr syl rpdivcl sylan2 rpre ancoms adantlr
      cz readdcl 3syl fopab2 sylib sylan remulcl syl6bb ad2ant2rl an32s adantrr
      mpbird ex sylbid cuz sstri mpan2 elnnuz fzfi sylbi cle 1z ax-mp 0re mp2an
      1re uzss syl5eqelr ctotbnd cbnd totbndbnd crrn cres rrnmet rrndm metssba2
      syl6eqr raleqdv 0fin id 0ss ral0 jctir sylancr wn df-ne r19.2z csqr ssel2
      anim1i anasss syl6reqr biimpa elbl adantrl rrntotbndlem1 biimpd imdistani
      a1d cmin eleq2i rrntotbndlem2 rpsqrcl resubcl rpne0 redivcl syl111anc 2re
      nnrp 2ne0 rereccli sylancl flcl zre ad2antlr elbl2 syl31anc sylanl1 eleq2
      expr imp eluni2 sylibr an4s ssrdv eqeq1d cin blssp inss1 sseq1 syl5ibrcom
      sylbird sstr expcom sylsyld visset eqeq1 anbi2d exbidv bitri fzssuz uzssz
      elab fss simprr opreq1 eqeq2d 19.23adv syl5bi r19.21aiv jctird cpw simplr
      zssre cn0 flge0nn0 nn0p1nn ax-1cn negnegi znegcl lt01 ltleii le0neg2 mpbi
      rpge0 renegcli letri eluz mpbiri sseli uzneg sseldd xpfi pwfi cvv mapsspw
      mapval ssfi firnfi4 adantllr adantlrl jctild syl6 r19.21adva syl5 expdimp
      r19.23advv sylan2br pm2.61dan metres syl5eqel sstotbnd 3imtr4d impbid2
      isbnd2 ) BVDHZDCIZJZAUUAHZAUUBHZAAUEZUEZVVNUMZUUCVVJVVNUAKZUBKZAUNLMZNZUB
      OUOZUAVVNUPZDUCKZUQZIZUDKZUFKZUGKZBUUDLZUNLZMZNZUFVWHUEUEZUOZUDVWBUPZJZUC
      PUOZUGOUPZVVLVVKVVJVWAVVTUADUPZVWQVVJVVTUAVVNDVVJVVNVWHDDURUUEZUEZUEZDVVJ
      VVMVWTVVJAVWSAVWSNVVJFUSUTUTZVVJVWHVAHZDVWLIZDVXANVVHVXCVVIBUUFZVBZVVHVXD
      VVIVVHVWLCDVVHVWLQRBVCMZVOMZCBUUGZEUUIVEVFZVWHVWLDVWLUMZUUHSZVGUUJVVJDTNZ
      VWRVWQVHZVVJVXMJZVWQVWRVXOVWPUGOVXOVWPVWGOHZVXOTPHDTUQZIZVWMUDTUPZJZVWPUU
      KVXOVXRVXSVXMVXRVVJVXMDTVXQVXMUULTVXQIVXMVXQUUMUSVIVJVWMUDUUNUUOVWOVXTUCT
      PVWBTNZVWDVXRVWNVXSVYAVWCVXQDVWBTVKVEVWMUDVWBTVLVMVPUUPVBVNUVKVXMUUQVVJDT
      VQZVXNDTUURVVJVYBVWRVWQVYBVWRJVVTUADUOVVJVWQVVTUADUUSVVJVVSVWQUAUBDOVVJVV
      PDHZVVQOHZJZVVSVWQVHVVJVYEJZVVSVWPUGOVYFVXPJZVVSVXGVVQVWGBUUTLZVRMZVRMZVS
      LZRVTMZWAZVYLVCMZUHKZWBZUIKZGKZVXGHZUJKZVYRVVPLZVYIVYRVYOLZWCMZVTMZNJGUJW
      DZVWGVWIMZNZJZUHWEZUIWFZPHZDWUJUQZIZVWMUDWUJUPZJZJVWPVYGVVSWUOWUKVYGVVSWU
      MWUNVYGVVPVVQVWIMZWULIZVVSDWUPIZWUMVYGUFWUPWULVYFVVHVVPCHZVYDJZJZVXPVWFWU
      PHZVWFWULHZVHZVVHVVIVYEWVAVVIVYEJWUTVVHVVIVYCVYDWUTVVIVYCJZWUSVYDDCVVPUVA
      ZUVBUVCWGWHVVHVXPWUTWVDVVHWUSVXPVYDWVDVVHWUSJZVXPVYDJJZWVBVWFVWLHZVVPVWFV
      WHMVVQWIWJZJZWVCWVGVYDWVBWVKWKZVXPWVGVXCVVPVWLHZJVVQQHWLVVQWIWJJWVLVYDWVG
      VXCWVMVVHVXCWUSVXEVBVVHWUSWVMVVHCVWLVVPVVHVWLVXHCVXIEUVDWMUVEWNVVQWOVWFVW
      HVVPVVQVWLVXKUVFWPUVGWVHWVKWVCWVHWVKJZVWFUKKZHZUKWUJUOZWVCWVNVYSVYTWUAVYI
      VYRULKZVXGHZVWBWVRVWFLZWVRVVPLZUVLMZVYIVRMZRXBVRMZVTMZVSLZNJULUCWDZLZWCMZ
      VTMZNJGUJWDZVWGVWIMZWUJHVWFWWLHZWVQUAUFUCUJUHGULABCDUBUIUGEFUVHWVHWVIWWMW
      VJWVGVXPWVIWWMVYDWVGVXPJZWVIJWWNVWFVXHHZJWWMWWNWVIWWOVVHWVIWWOVHWUSVXPVVH
      WVIWWOVVHVWLVXHVWFVXIWMZUVIWQUVJWVGVVHVVPVXHHZJZVXPWWOWWMWUSWWQVVHWUSWWQC
      VXHVVPEUVMZWRWGWWRVXPJWWOWWMVVHVXPWWQWWOWWMVHVVHVXPJZWWQWWOWWMWWTWWQWWOJZ
      JZWWMWWKVWFVWHMVWGWIWJZUAUFUCUJGULABCDUGEFUVNWXBVXCWWKVWLHZWVIVWGQHWLVWGW
      IWJJZWWMWXCWKVVHVXCVXPWXAVXEWQWXBWXDVXGQWWKWBZWXBWWJQHZGVXGUPWXFWXBWXGGVX
      GWXBVYSJZWUAQHZWWIQHZWXGWWTWWQVYSWXIWWOWWQVYSWXIWWTWWQVXGQVVPWBZVYSWXIQVX
      GVVPWSRBVCWTZXAZVXGQVYRVVPXCZXDXEXFWXHVYIQHZWWHQHZWXJWWTWXOWXAVYSVXPVVHWX
      OVXPVVHJZVYIOHZWXOVVHVXPVYHOHZWXRVVHBOHWXSBUWABUVOXGVWGVYHXHXIZVYIXJXGXKZ
      WQWXBVXGQWWGWBZVYSWXPWXBWWFQHZULVXGUPWYBWXBWYCULVXGWXBWVSJZWWEQHZWWFXMHWY
      CWYDWWCQHZWWDQHWYEWYDWWBQHZWXOVYIWLVQZWYFWXAWVSWYGWWTWXAWVSJWVTQHZWWAQHZW
      YGWWOWVSWYIWWQWWOVXGQVWFWBWVSWYIQVXGVWFWSWXLXAVXGQWVRVWFXCXDXEWWQWVSWYJWW
      OWWQWXKWVSWYJWXMVXGQWVRVVPXCXDXLWVTWWAUVPSXEWWTWXOWXAWVSWYAWQWWTWYHWXAWVS
      VXPVVHWYHWXQWXRWYHWXTVYIUVQXGXKWQWWBVYIUVRUVSXBUVTUWBUWCWWCWWDXNUWDWWEUWE
      WWFUWFXOVNULUCVXGQWWFWWGWWGUMXPXQVXGQVYRWWGXCXRVYIWWHXSSWUAWWIXNSVNGUJVXG
      QWWJWWKWWKUMXPXQWXBWXDWWKVXHHWXFWXBVWLVXHWWKVVHVWLVXHNVXPWXAVXIWQWMQVXGWW
      KWSWXLXAXTYDVVHWWOWVIVXPWWQVVHWVIWWOWWPVFYAVXPWXEVVHWXAVWGWOUWGVWFVWHWWKV
      WGVWLVXKUWHUWIYDUWLYBUWMUWJXGXFYCWVPWWMUKWWLWUJWVOWWLVWFUWKVPSUKVWFWUJUWN
      UWOYEYFUWPYBXRUWQVYFVVSWURVHVXPVYFVVSDVVRNZWURVYFDVVNVVRVVJDVVNNVYEVVJDVX
      AVVNVXLVXBVGVBUWRVYFWURWYKVVRWUPIZVVJVXCVXDJZVYEWYLVVJVXCVXDVXFVXJWNWYMVY
      EJZVVRWUPDUWSZWUPVVQDVWHAVWLVVPVXKFUWTWYOWUPIWYNWUPDUXAUSVIXRDVVRWUPUXBUX
      CUXDVBWURWUQWUMDWUPWULUXEUXFUXGVYGVWMUDWUJVWEWUJHVYPVWEWUFNZJZUHWEZVYGVWM
      WUIWYRUIVWEUDUXHVYQVWENZWUHWYQUHWYSWUGWYPVYPVYQVWEWUFUXIUXJUXKUXOVYGWYQVW
      MUHVYGWYQVWMVYGWYQJWUEVWLHZWYPVWMVYGVYPWYTWYPVYGVYPJZWYTVXGQWUEWBZXUAWUDQ
      HZGVXGUPXUBXUAXUCGVXGVYGVYPVYSXUCVYGVYPVYSJZJWXIWUCQHZXUCVYFVYSWXIVXPVYPV
      VJVYCVYSWXIVYDVVJVYCJWXKVYSWXIVVHVVIVYCWXKWVEVVHWUSWXKWVFWUSWXKVVHWUSWXKW
      USWWQWXKWWSWXMUXLWRVJXIWHWXNXRXFYAVYGWXOWUBQHZXUEXUDVVJVXPWXOVYEVVHVXPWXO
      VVIWYAXLXLVYPVXGQVYOWBZVYSXUFVYPVYNQIXUGVYNXMQVYNVYMYGLZXMVYMVYLUXMVYMUXN
      YHUYFYHVXGVYNQVYOUXPYIVXGQVYRVYOXCXRVYIWUBXSWPWUAWUCXNSWHVNGUJVXGQWUDWUEW
      UEUMXPXQVYFWYTXUBWKZVXPVYPVVHXUIVVIVYEVVHWYTWUEVXHHXUBVVHVWLVXHWUEVXIWMQV
      XGWUEWSWXLXAXTWQWQYDYCVYGVYPWYPUXQVWKWYPUFWUEVWLVWFWUENVWJWUFVWEVWFWUEVWG
      VWIUXRUXSVPSYEUXTUYAUYBUYCVVJVYDVXPWUKVYCVVHVYDVXPWUKVVIVVHVYDJVXPJZVXGVY
      NURZUYDZPHZVYPUHWFZPHWUKXUJXUKPHZXUMXUJVXGPHZVYNPHZXUOVVHXUPVYDVXPVVHBRYG
      LZHXUPBYJRBYKYLWQXUJVYLVDHZVYLXUHHXUQXUJVYJOHZVYKUYGHZXUSXUJVYDWXRXUTVVHV
      YDVXPUYEVVHVXPWXRVYDVXPVVHWXRWXTXKXLVVQVYIXHSXUTVYJQHWLVYJYMWJXVAVYJXJVYJ
      UYQVYJUYHSVYKUYIXOXUSXURXUHVYLXUSRXUHHXURXUHIXUSRRWAZWAZXUHRUYJUYKXUSVYLX
      VBYGLZHZXVCXUHHXUSVYLXURHZXVEVYLYJZXURXVDVYLRXVDHZXURXVDIXVBXMHZRXMHZXVHX
      VJXVIYNRUYLYOYNXVIXVJJXVHXVBRYMWJZXVBWLYMWJZWLRYMWJZXVKXVMXVLWLRYPYRUYMUY
      NZRQHXVMXVLWKYRRUYOYOUYPXVNXVBWLRRYRUYRYPYRUYSYQXVBRUYTVUAYQXVBRYSYOVUBYL
      XVBVYLVUCXGYTVYMRYSXGXUSXVFXVGWRVUDVYMVYLYKXOVXGVYNVUESXUKVUFXQXUMXUNVYNV
      XGVOMZPVYNVXGUHVYMVYLVCWTWXLVUIXUMXVOXULIZXVOPHVXGVUGHXVPWXLVYNVXGVUGVUHY
      OXULXVOVUJYIYTVYPUIUHWUFVUKXOVULVUMVUNVWOWUOUCWUJPVWBWUJNZVWDWUMVWNWUNXVQ
      VWCWULDVWBWUJVKVEVWMUDVWBWUJVLVMVPVUOVUPYEVUSVUQVURVUTVVAYFVVJVXCAVAHVVLV
      WAWKVXFVXCAVWSVAFVWHDVVBVVCUAAVVNUBVVOVVGXOVVJVXCVXDVVKVWQWKVXFVXJUFUCVWH
      AVWLDUDUGVXKFVVDSVVEVVF $.
      $( [2-Sep-2009] $)
  $}

  ${
    rrnheibor.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrnheibor.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    rrnheibor.3 $e |- T = ( MetOpen ` M ) $.
    rrnheibor.4 $e |- U = ( MetOpen ` ( Rn ` N ) ) $.
    $( Heine-Borel theorem for Euclidean space.  A subset of Euclidean space is
       compact iff it is closed and bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnheibor $p |- ( ( N e. NN /\ Y C_ X ) ->
                      ( T e. Comp <-> ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( wcel wss wa cms cfv cme wb syl adantr cdm ctotbnd ccld cbnd crrn rrnmet
      cn ccomp cxp cres metres syl5eqel eqid heibor eleq1i rrncms cr c1 co cmap
      cfz rrndm syl6reqr sseq2d biimpa cmsss syl11anc syl5bb rrntotbnd anbi12d
      bitrd ) DUFKZFELZMZAUGKZCNKZCUAKZMZFBUBOKZCUCKZMVMCPKZVNVQQVKVTVLVKDUDOZP
      KZVTDUEWBCWAFFUHUIZPHWAFUJUKRSACCTTZIWDULUMRVMVOVRVPVSVOWCNKZVMVRCWCNHUNV
      MWANKZFWATTZLZWEVRQVKWFVLDUOSVKVLWHVKEWGFVKWGUPUQDUTURUSUREDVAGVBVCVDWABW
      GFWGULJVEVFVGCDEFGHVHVIVJ $.
      $( [2-Sep-2009] $)
  $}

