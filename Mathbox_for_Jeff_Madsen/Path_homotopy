$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Path homotopy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$c PHtpy $.
$c =~ph $.
$( Extend class notation with the class of path homotopies between
two continuous functions. $)
cphtpy $a class PHtpy $.
$( Extend class notation with the path homotopy relation. $)
cphtpc $a class =~ph $.

${
  $d x y f g z h s $.
  $( Define the function which takes a topological space ` X ` and two
  continuous functions ` F : II --> X ` and ` G : II --> X ` and returns
  the class of path homotopies from ` F ` to ` G ` . $)
  df-phtpy $a |- PHtpy = { <. x , y >. | ( x e. Top /\ y =
      { <. <. f , g >. , z >. | ( ( ( f e. ( II Cn x ) /\ g e. ( II Cn x ) ) /\
              ( ( f ` 0 ) = ( g ` 0 ) /\ ( f ` 1 ) = ( g ` 1 ) ) ) /\ z =
                    { h e. ( ( II tX II ) Cn x ) | A. s e. ( 0 [,] 1 )
                  ( ( ( s h 0 ) = ( f ` s ) /\ ( s h 1 ) = ( g ` s ) ) /\
              ( ( 0 h s ) = ( f ` 0 ) /\ ( 1 h s ) = ( f ` 1 ) ) ) } ) } ) } $.
$}

${
  $d J x y f g z h s $.
  $( Value of the path homotopy function on a topology. $)
  phtpyfval $p |- ( J e. Top -> ( PHtpy ` J ) =
      { <. <. f , g >. , z >. | ( ( ( f e. ( II Cn J ) /\ g e. ( II Cn J ) ) /\
              ( ( f ` 0 ) = ( g ` 0 ) /\ ( f ` 1 ) = ( g ` 1 ) ) ) /\ z =
                    { h e. ( ( II tX II ) Cn J ) | A. s e. ( 0 [,] 1 )
                  ( ( ( s h 0 ) = ( f ` s ) /\ ( s h 1 ) = ( g ` s ) ) /\
              ( ( 0 h s ) = ( f ` 0 ) /\ ( 1 h s ) = ( f ` 1 ) ) ) } ) } ) $=
    ( vx vy cv cii ccn co wcel wa cc0 cfv wceq c1 cicc wral ctx crab copab2
    ctop cphtpy opreq2 eleq2d anbi12d anbi1d rabeq syl eqeq2d oprabbidv
    df-phtpy oprex wmo moeq moani a1i anass oprabbii oprabex fvopab4 ) GHEBIZJG
    IZKLZMZCIZVFMZNZOVDPZOVHPQRVDPZRVHPQNZNZAIZFIZODIZLVPVDPQVPRVQLVPVHPQNOVPVQ
    LVKQRVPVQLVLQNNFORSLTZDJJUALZVEKLZUBZQZNZBCAUCVDJEKLZMZVHWDMZNZVMNZVOVRDVSE
    KLZUBZQZNZBCAUCZUDUEVEEQZWCWLBCAWNVNWHWBWKWNVJWGVMWNVGWEVIWFWNVFWDVDVEEJKUF
    ZUGWNVFWDVHWOUGUHUIWNWAWJVOWNVTWIQWAWJQVEEVSKUFVRDVTWIUJUKULUHUMGHABCDFUNVM
    WKNZBCAWDWDWMJEKUOZWQWPAUPWGWKVMAAWJUQURUSWLWGWPNBCAWGVMWKUTVAVBVC $.
    $( [2-Sep-2009] $)
$}

${
  $d J f g z h s $. $d F f g z h s $. $d G f g z h s $.
  $( The class of path homotopies between two continuous functions. $)
  phtpyval $p |- ( ( ( J e. Top /\ F e. ( II Cn J ) /\ G e. ( II Cn J ) ) /\
                  ( ( F ` 0 ) = ( G ` 0 ) /\ ( F ` 1 ) = ( G ` 1 ) ) ) ->
            ( F ( PHtpy ` J ) G ) = { h e. ( ( II tX II ) Cn J ) |
      A. s e. ( 0 [,] 1 ) ( ( ( s h 0 ) = ( F ` s ) /\ ( s h 1 ) = ( G ` s ) )
              /\ ( ( 0 h s ) = ( F ` 0 ) /\ ( 1 h s ) = ( F ` 1 ) ) ) } ) $=
    ( vf vg vz ctop wcel cii ccn co w3a cc0 cfv wceq c1 wa cphtpy cv cicc wral
    ctx crab copab2 phtpyfval opreqd 3ad2ant1 adantr eqid wi oprex rabex fveq1
    eqeq1d anbi12d eqeq2d anbi1d ralbidv rabbisdv anbi2d eqeq1 wmo moeq moani
    a1i anass oprabbii oprabvali 3adant1 mpan2i imp eqtrd ) DIJZBKDLMZJZCVPJZNZ
    OBPZOCPZQZRBPZRCPZQZSZSBCDTPZMZBCFUAZVPJGUAZVPJSZOWIPZOWJPZQZRWIPZRWJPZQZSZ
    SHUAZEUAZOAUAZMZWTWIPZQZWTRXAMZWTWJPZQZSZOWTXAMZWLQZRWTXAMZWOQZSZSZEORUBMZU
    CZAKKUDMZDLMZUEZQZSZFGHUFZMZXBWTBPZQZXEWTCPZQZSZXIVTQZXKWCQZSZSZEXOUCZAXRUE
    ZVSWHYCQZWFVOVQYOVRVOWGYBBCHFGADEUGUHUIUJVSWFYCYNQZVSWFYNYNQZYPYNUKVQVRWFYQ
    SZYPULVOWRXTSZVTWMQZWCWPQZSZWSYEXGSZYKSZEXOUCZAXRUEZQZSWFWSYNQZSYRFGHBCYNVP
    VPYBYMAXRXQDLUMUNWIBQZWRUUBXTUUGUUIWNYTWQUUAUUIWLVTWMOWIBUOZUPUUIWOWCWPRWIB
    UOZUPUQUUIXSUUFWSUUIXPUUEAXRUUIXNUUDEXOUUIXHUUCXMYKUUIXDYEXGUUIXCYDXBWTWIBU
    OURUSUUIXJYIXLYJUUIWLVTXIUUJURUUIWOWCXKUUKURUQUQUTVAURUQWJCQZUUBWFUUGUUHUUL
    YTWBUUAWEUULWMWAVTOWJCUOURUULWPWDWCRWJCUOURUQUULUUFYNWSUULUUEYMAXRUULUUDYLE
    XOUULUUCYHYKUULXGYGYEUULXFYFXEWTWJCUOURVBUSUTVAURUQUUHUUHYQWFWSYNYNVCVBYSHV
    DWKXTWRHHXSVEVFVGYAWKYSSFGHWKWRXTVHVIVJVKVLVMVN $.
    $( [2-Sep-2009] $)
$}

${
  $d J h s $. $d F h s $. $d G h s $. $d H h s $.
  $( Membership in the class of path homotopies between two continuous
  functions. $)
  isphtpy $p |- ( ( ( J e. Top /\ F e. ( II Cn J ) /\ G e. ( II Cn J ) ) /\
                  ( ( F ` 0 ) = ( G ` 0 ) /\ ( F ` 1 ) = ( G ` 1 ) ) ) ->
          ( H e. ( F ( PHtpy ` J ) G ) <-> ( H e. ( ( II tX II ) Cn J ) /\
      A. s e. ( 0 [,] 1 ) ( ( ( s H 0 ) = ( F ` s ) /\ ( s H 1 ) = ( G ` s ) )
              /\ ( ( 0 H s ) = ( F ` 0 ) /\ ( 1 H s ) = ( F ` 1 ) ) ) ) ) ) $=
    ( vh ctop wcel cii ccn co w3a cc0 cfv wceq c1 wa cphtpy cv cicc wral ctx
    crab phtpyval eleq2d opreq eqeq1d anbi12d ralbidv elrab syl6bb ) DGHAIDJKZH
    BULHLMANZMBNOPANZPBNOQQZCABDRNKZHCESZMFSZKZUQANZOZUQPURKZUQBNZOZQZMUQURKZUM
    OZPUQURKZUNOZQZQZEMPTKZUAZFIIUBKDJKZUCZHCVNHUQMCKZUTOZUQPCKZVCOZQZMUQCKZUMO
    ZPUQCKZUNOZQZQZEVLUAZQUOUPVOCFABDEUDUEVMWGFCVNURCOZVKWFEVLWHVEVTVJWEWHVAVQV
    DVSWHUSVPUTUQMURCUFUGWHVBVRVCUQPURCUFUGUHWHVGWBVIWDWHVFWAUMMUQURCUFUGWHVHWC
    UNPUQURCUFUGUHUHUIUJUK $.
    $( [2-Sep-2009] $)
$}

${
  $d J s x y z $. $d F s x y z $. $d H s $.
  phtpyid.1 $e |- H = { <. <. x , y >. , z >. | ( ( x e. ( 0 [,] 1 )
                              /\ y e. ( 0 [,] 1 ) ) /\ z = ( F ` x ) ) } $.
  $( A homotopy from a path to itself. $)
  phtpyid $p |- ( ( J e. Top /\ F e. ( II Cn J ) ) ->
                                        H e. ( F ( PHtpy ` J ) F ) ) $=
    ( vs ctop wcel cii ccn co wa cphtpy cfv ctx cv cc0 wceq c1 cicc wral c1st
    cxp cres ccom copab2 eqidd fvres fveq2d adantl wfun cdm fvco cuni wf iitop
    iiuni eqcomi eqid cnf mp3an1 ffun syl adantr f1stres ax-mp a1i fdmi eleq2i
    biimpri syl3anc c2nd cop elxp6 fveq2 df-opr syl6eqr fvex oprabval2 eqtrd
    sylbi 3eqtr4rd r19.21aiva jca wfn wb eqfnfv fnoprab2 crn wss fnco ffn frn
    sylancr mpbird syl5eq w3a cnco txtop mp2an pm3.26 3jca pm3.27 tx1cn jctil
    sylanc eqeltrd cr cle wbr 0re 1re lt01 ltleii lbicc2 mp3an mpan2 ubicc2
    mpan jca31 isphtpy sylan32c ) FIJZDKFLMJZNZEDDFOPMJZEKKQMZFLMZJZHRZSEMYLDPZ
    TZYLUAEMYMTZNSYLEMSDPZTZUAYLEMUADPZTZNZNZHSUAUBMZUCZNZYGYKUUCYGEDUDUUBUUBUE
    ZUFZUGZYJYGARZUUBJBRZUUBJNCRUUHDPZTNABCUHZUUGEYGUUKUUGTZUUEUUETZYLUUKPZYLUU
    GPZTZHUUEUCZNZYGUUMUUQYGUUEUIYGUUPHUUEYGYLUUEJZNZYLUUFPZDPZYLUDPZDPZUUOUUNU
    USUVBUVDTYGUUSUVAUVCDYLUUEUDUJUKULDUMZUUFUMZYLUUFUNZJZUUOUVBTUUTYLDUUFUOYGU
    VEUUSYGUUBFUPZDUQZUVEKIJZYEYFUVJURDKFUUBUVIKUPUUBUSUTZUVIVAVBVCZUUBUVIDVDVE
    VFUVFUUTUUEUUBUUFUQZUVFUUBUUBVGZUUEUUBUUFVDVHVIUUSUVHYGUVHUUSUVGUUEYLUUEUUB
    UUFUVOVJVKVLULVMUUSUUNUVDTZYGUUSYLUVCYLVNPZVOZTZUVCUUBJUVQUUBJNZNZUVPYLUUBU
    UBVPUWAUUNUVCUVQUUKMZUVDUWAUUNUVRUUKPZUWBUVSUUNUWCTUVTYLUVRUUKVQVFUVCUVQUUK
    VRVSUVTUWBUVDTUVSABCUVCUVQUUBUUBUUJUVDUUKUVDUVCDVTUUHUVCDVQUUIUVQTUVDUIUUKV
    AZWAULWBWCULWDWEWFUUKUUEWGUUGUUEWGZUULUURWHYGHUUEUUEUUKUUGWIABCUUBUUBUUJUUK
    UUHDVTUWDWJDUUBWGZUUFUUEWGZUUFWKUUBWLZUWEYGUUBUUEDUUFWMYGUVJUWFUVMUUBUVIDWN
    VEUWGYGUVNUWGUVOUUEUUBUUFWNVHVIUWHYGUVNUWHUVOUUEUUBUUFWOVHVIVMWPWQGWRYIIJZU
    VKYEWSUUFYIKLMJZYFNUUGYJJYGUUFDYIKFWTYGUWIUVKYEUWIYGUVKUVKUWIURURKKYIYIVAZX
    AXBVIUVKYGURVIYEYFXCZXDYGYFUWJYEYFXEZUVKUVKUWJURURKKYIUUBUUBUUEUWKUVLUVLUUE
    VAXFXBXGXHXIYGUUAHUUBYLUUBJZUUAYGUWNYNYOYTUWNSUUBJZYNSXJJZUAXJJZSUAXKXLZUWO
    XMXNSUAXMXNXOXPZSUAXQXRZABCYLSUUBUUBUUJYMEYMYLDVTZUUHYLDVQZUUISTYMUIGWAXSUW
    NUAUUBJZYOUWPUWQUWRUXCXMXNUWSSUAXTXRZABCYLUAUUBUUBUUJYMEYMUXAUXBUUIUATYMUIG
    WAXSUWNYQYSUWOUWNYQUWTABCSYLUUBUUBUUJYPEYPSDVTUUHSDVQUUIYLTZYPUIGWAYAUXCUWN
    YSUXDABCUAYLUUBUUBUUJYREYRUADVTUUHUADVQUXEYRUIGWAYAWFYBULWEWFYEYFYFWSYPYPTY
    RYRTYHUUDWHYGYGYEYFYFUWLUWMUWMXDYGYPUIYGYRUIDDEFHYCYDWQ $.
    $( [2-Sep-2009] $)
$}

${
  $d J s t w x y z $. $d F s t w x y z $. $d H s t w x y z $.
  $d G s t w x y z $. $d K s t w $.
  phtpycom.1 $e |- K = { <. <. x , y >. , z >. | ( ( x e. ( 0 [,] 1 )
                          /\ y e. ( 0 [,] 1 ) ) /\ z = ( x H ( 1 - y ) ) ) } $.
  $( Given a homotopy from ` F ` to ` G ` , produce a homotopy from
  ` G ` to ` F ` . $)
  phtpycom $p |- ( ( ( J e. Top /\ F e. ( II Cn J ) /\ G e. ( II Cn J ) ) /\
                  ( ( F ` 0 ) = ( G ` 0 ) /\ ( F ` 1 ) = ( G ` 1 ) ) ) ->
              ( H e. ( F ( PHtpy ` J ) G ) -> K e. ( G ( PHtpy ` J ) F ) ) ) $=
    ( vs vt vw ctop wcel cii ccn co w3a cc0 cfv wceq c1 wa ctx cv cicc wral
    cphtpy copab cmin cop copab2 ccom cxp cuni wf wfn iitop eqid txtop mp2an
    iiuni eqcomi txuni cnf mp3an1 ffn opelxpi iirev sylan2 id visset fvopab4
    adantr opreq2 oprex adantl opeq12d eqeq2d pm5.32i oprabbii df-opr eqeq2i
    anbi2i eqtri oprabco 3syl cnco a1i pm3.26 3jca pm3.27 pm3.2i cid cres
    df-id equcom opabbii reseq1 ax-mp resopab eqtr2i idcn eqeltri ccncf cc wss
    cr 0re 1re iccssre axresscn sstri ssid 3pm3.2i eqeltrd rgen resopab2 ax1cn
    sub2cncf wi rescncf mp3an eqeltrri cncffvrn mp2 cabs df-ii cncfmet eleqtri
    2txcn jctil sylanc 3ad2antl1 ad2ant2r opreq1 fveq2 eqeq12d anbi12d eqeq1d
    rcla4v imdistani cle wbr lt01 ltleii lbicc2 opreq2d oprabval2 mpan2
    subid1i opreq2i syl6eq ubicc2 subidi biimpar ancom2s adantrr syl mpan
    ad2antlr eqtr ancoms adantlr eqtrd adantllr adantrl adantlll jca anasss
    anassrs r19.21aiva ad2ant2l ex isphtpy wb 3simp1 3simp3 3simp2 eqcom
    anbi12i biimpi syl2an 3imtr4d ) GMNZDOGPQZNZEUWFNZRZSDTZSETZUAZUBDTZUBETZUA
    ZUCZUCZFOOUDQZGPQZNZJUEZSFQZUXADTZUAZUXAUBFQZUXAETZUAZUCZSUXAFQZUWJUAZUBUXA
    FQZUWMUAZUCZUCZJSUBUFQZUGZUCZHUWSNZKUEZSHQZUXSETZUAZUXSUBHQZUXSDTZUAZUCZSUX
    SHQZUWKUAZUBUXSHQZUWNUAZUCZUCZKUXOUGZUCZFDEGUHTZQNHEDUYOQNZUWQUXQUYNUWQUXQU
    CUXRUYMUWIUWTUXRUWPUXPUWEUWGUWTUXRUWHUWEUWTUCZHFAUEZUXONZBUEZUXONZUCZLUEZUY
    RUXAUXONZUXSUXAUAZUCJKUIZTZUYTVUDUXSUBUXAUJQZUAZUCJKUIZTZUKZUAZUCZABLULZUMZ
    UWSUYQUXOUXOUNZGUOZFUPZFVUQUQHVUPUAUWRMNZUWEUWTVUSOMNZVVAVUTURUROOUWRUWRUSZ
    UTVAZFUWRGVUQVURUWRUOZVUQVVAVVAVVDVUQUAURUROOUWRUXOUXOVVBOUOUXOVBVCZVVEVDVA
    VCVURUSVEVFVUQVURFVGABLCUXOUXOUYRUBUYTUJQZUKZVUQVUOHFUYSVVFUXONVVGVUQNVUAUY
    RVVFUXOUXOVHUYTVIVJVUNVUBVUCVVGUAZUCABLVUBVUMVVHVUBVULVVGVUCVUBVUGUYRVUKVVF
    UYSVUGUYRUAVUAJKUYRUXAUYRUXOVUFUXAUYRUAVKVUFUSAVLVMVNVUAVUKVVFUAUYSJKUYTVUH
    VVFUXOVUJUXAUYTUBUJVOVUJUSZUBUYTUJVPVMVQVRVSVTWAHVUBCUEZUYRVVFFQZUAZUCZABCU
    LVUBVVJVVGFTZUAZUCZABCULIVVMVVPABCVVLVVOVUBVVKVVNVVJUYRVVFFWBWCWDWAWEWFWGVU
    TVUTUWERVUOUWRUWRPQNZUWTUCVUPUWSNUYQVUOFUWRUWRGWHUYQVUTVUTUWEVUTUYQVVCWIZVV
    RUWEUWTWJWKUYQUWTVVQUWEUWTWLVVAVVAUCZVVSVUFOOPQZNZVUJVVTNZUCVVQVVAVVAURURWM
    ZVWCVWAVWBVUFWNUXOWOZVVTVWDVUEJKUIZUXOWOZVUFWNVWEUAVWDVWFUAWNUXAUXSUAZJKUIV
    WEJKWPVWGVUEJKJKWQWRWEWNVWEUXOWSWTVUEJKUXOXAXBVVAVWDVVTNUROUXOVVEXCWTXDVUJU
    XOUXOXEQZVVTUXOXFXGZXFXFXGZVWIRZVUCVUJTZUXONZLUXOUGZUCVUJUXOXFXEQZNVUJVWHNV
    WKVWNVWIVWJVWIUXOXHXFSXHNZUBXHNZUXOXHXGXIXJSUBXKVAXLXMZXFXNZVWRXOVWMLUXOVUC
    UXONVWLUBVUCUJQZUXOJKVUCVUHVWTUXOVUJUXAVUCUBUJVOVVIUBVUCUJVPVMVUCVIXPXQWMUX
    AXFNVUIUCJKUIZUXOWOZVUJVWOVWIVXBVUJUAVWRVUIJKUXOXFXRWTVXAXFXFXEQNZVXBVWONZU
    BXFNVXCXSJKUBVXAVXAUSXTWTVWJVWJVWIVXCVXDYAVWSVWSVWRXFXFUXOVXAYBYCWTYDLUXOXF
    UXOVUJYEYFVWIVWIVWHVVTUAVWRVWRUXOUXOYGUJUMVUQWOZVXEOOVXEUSZVXFYHYHYIVAYJWMA
    BLOOUWRVUFVUJVUOOOUWRUXOUXOVVBVVBVVEVVEVUOUSYKYCYLYMXPYNYOUWPUXPUYMUWIUWTUW
    PUXPUCUYLKUXOUWPUXPUXSUXONZUYLUWPVXGUXPUYLUWPVXGUXPUCZUCUYFUYKVXHUYFUWPVXHV
    XGUXSSFQZUYDUAZUXSUBFQZUYAUAZUCZSUXSFQZUWJUAZUBUXSFQZUWMUAZUCZUCZUCUYFVXGUX
    PVXSUXNVXSJUXSUXOVWGUXHVXMUXMVXRVWGUXDVXJUXGVXLVWGUXBVXIUXCUYDUXAUXSSFYPUXA
    UXSDYQYRVWGUXEVXKUXFUYAUXAUXSUBFYPUXAUXSEYQYRYSVWGUXJVXOUXLVXQVWGUXIVXNUWJU
    XAUXSSFVOYTVWGUXKVXPUWMUXAUXSUBFVOYTYSYSUUAUUBVXGVXMUYFVXRVXGVXLVXJUYFVXGUY
    FVXLVXJUCVXGUYBVXLUYEVXJVXGUXTVXKUYAVXGUXTUXSUBSUJQZFQZVXKVXGSUXONZUXTVYAUA
    VWPVWQSUBUUCUUDZVYBXIXJSUBXIXJUUEUUFZSUBUUGYCZABCUXSSUXOUXOVVKVYAHUXSVVFFQZ
    UXSVXTFVPUYRUXSVVFFYPZUYTSUAVVFVXTUXSFUYTSUBUJVOUUHIUUIUUJVXTUBUXSFUBXSUUKU
    ULUUMYTVXGUYCVXIUYDVXGUYCUXSUBUBUJQZFQZVXIVXGUBUXONZUYCVYIUAVWPVWQVYCVYJXIX
    JVYDSUBUUNYCZABCUXSUBUXOUXOVVKVYIHVYFUXSVYHFVPVYGUYTUBUAVVFVYHUXSFUYTUBUBUJ
    VOUUHIUUIUUJVYHSUXSFUBXSUUOUULUUMYTYSUUPUUQUURUUSVQUWPVXGUBUXSUJQZSFQZVYLDT
    ZUAZVYLUBFQZVYLETZUAZUCZSVYLFQZUWJUAZUBVYLFQZUWMUAZUCZUCZUCUYKVXHUWPVXGWUEU
    YKUWPVXGUCZWUDUYKVYSWUFWUDUCUYHUYJUWLVXGWUDUYHUWOUWLVXGUCZWUAUYHWUCWUGWUAUC
    UYGVYTUWKVXGUYGVYTUAZUWLWUAVYBVXGWUHVYEABCSUXSUXOUXOVVKVYTHSVVFFQSVYLFVPUYR
    SVVFFYPUYTUXSUAZVVFVYLSFUYTUXSUBUJVOZUUHIUUIUUTUVAUWLWUAVYTUWKUAZVXGWUAUWLW
    UKVYTUWJUWKUVBUVCUVDUVEUURUVFUWOVXGWUDUYJUWLUWOVXGUCZWUCUYJWUAWULWUCUCUYIWU
    BUWNVXGUYIWUBUAZUWOWUCVYJVXGWUMVYKABCUBUXSUXOUXOVVKWUBHUBVVFFQUBVYLFVPUYRUB
    VVFFYPWUIVVFVYLUBFWUJUUHIUUIUUTUVAUWOWUCWUBUWNUAZVXGWUCUWOWUNWUBUWMUWNUVBUV
    CUVDUVEUVGUVHUVIUVGUVJVXGUXPWUEVXGVYLUXONUXPWUEYAUXSVIUXNWUEJVYLUXOUXAVYLUA
    ZUXHVYSUXMWUDWUOUXDVYOUXGVYRWUOUXBVYMUXCVYNUXAVYLSFYPUXAVYLDYQYRWUOUXEVYPUX
    FVYQUXAVYLUBFYPUXAVYLEYQYRYSWUOUXJWUAUXLWUCWUOUXIVYTUWJUXAVYLSFVOYTWUOUXKWU
    BUWMUXAVYLUBFVOYTYSYSUUAUUSUUBVJUVIUUQUVKUVLUVMUVIUVNDEFGJUVOUWEUWHUWGRUWKU
    WJUAZUWNUWMUAZUCZUYPUYNUVPUWIUWPEDHGKUVOUWIUWEUWHUWGUWEUWGUWHUVQUWEUWGUWHUV
    RUWEUWGUWHUVSWKUWPWURUWLWUPUWOWUQUWJUWKUVTUWMUWNUVTUWAUWBUWCUWD $.
    $( [2-Sep-2009] $)
$}

${
  $d J r s t u v w x y z $. $d F r s t u v w x y z $. $d G r s t u v w x y z $.
  $d H r s t u v w x y z $. $d M r s t u v w $. $d K r s t u v w x y z $.
  $d L r s t u v w x y z $. $d X r s t u v w x y z $. $d Y r s t u v w x y z $.
  phtpyco.1 $e |- M = { <. <. x , y >. , z >. | ( ( x e. ( 0 [,] 1 ) /\
                    y e. ( 0 [,] 1 ) ) /\ z = if ( y <_ ( 1 / 2 ) ,
                    ( x K ( 2 x. y ) ) , ( x L ( ( 2 x. y ) - 1 ) ) ) ) } $.

  $( Lemma for ~ phtpyco . $)
  phtpycolem1 $p |- ( ( X e. ( 0 [,] 1 ) /\ Y e. ( 0 [,] ( 1 / 2 ) ) )
                                  -> ( X M Y ) = ( X K ( 2 x. Y ) ) ) $=
    ( cc0 c1 cicc co wcel c2 cdiv wa cle wbr cmul cmin cif wceq cv oprex ifex
    opreq1 ifeq1d ifeq2d eqtrd breq1 opreq2 opreq2d opreq1d ifbieq12d
    oprabval2 cr wss 0re 1re pm3.2i 2re 2ne0 rereccli leidi halflt1 ltleii
    iccss mp3an sseli sylan2 w3a wb elicc2 mp2an biimpi 3simp3d iftrue syl
    adantl ) GJKLMZNZHJKOPMZLMZNZQGHFMZHWCRSZGOHTMZDMZGWHKUAMZEMZUBZWIWBHWANWFW
    LUCWEABCGHWAWABUDZWCRSZAUDZOWMTMZDMZWOWPKUAMZEMZUBZWLFWNGWPDMZGWREMZUBZWGWI
    WKGWHDUEGWJEUEUFWOGUCZWTWNXAWSUBXCXDWNWQXAWSWOGWPDUGUHXDWNWSXBXAWOGWREUGUIU
    JWMHUCZWNWGXAXBWIWKWMHWCRUKXEWPWHGDWMHOTULZUMXEWRWJGEXEWPWHKUAXFUNUMUOIUPWD
    WAHJUQNZKUQNZQXGWCUQNZQJJRSZWCKRSZQWDWAURXGXHUSUTVAXGXIUSOVBVCVDZVAXJXKJUSV
    EWCKXLUTVFVGVAJKJWCVHVIVJVKWEWLWIUCZWBWEWGXMWEHUQNZJHRSZWGWEXNXOWGVLZXGXIWE
    XPVMUSXLJWCHVNVOVPVQWGWIWKVRVSVTUJ $.
    $( [2-Sep-2009] $)

  $( Lemma for ~ phtpyco . $)
  phtpycolem2 $p |- ( ( A. w e. ( 0 [,] 1 ) ( w K 1 ) = ( w L 0 ) /\
                      X e. ( 0 [,] 1 ) /\ Y e. ( ( 1 / 2 ) [,] 1 ) ) ->
                              ( X M Y ) = ( X L ( ( 2 x. Y ) - 1 ) ) ) $=
    ( cv c1 co cc0 wceq cicc wral wcel c2 cdiv w3a cle wbr cmul cmin cif oprex
    ifex opreq1 ifeq1d ifeq2d eqtrd breq1 opreq2 opreq2d opreq1d ifbieq12d
    oprabval2 cr wa wss 0re 1re pm3.2i 2re 2ne0 rereccli halfgt0 ltleii leidi
    iccss mp3an sseli sylan2 3adant1 wn wo iffalse adantl mpbiri iftrue syl
    2cn recidi syl6eq opreq1i ax1cn subidi eqtri eqeq12d biimparc jaodan
    rcla4cva wb elicc2 mp2an clt leloe mpan biimpa ltnle adantr eqcom a1i
    orbi12d mpbid 3adant3 sylbi syl2an 3impa ) DKZLEMZXKNFMZOZDNLPMZQZHXORZILST
    MZLPMZRZUAHIGMZIXRUBUCZHSIUDMZEMZHYCLUEMZFMZUFZYFXQXTYAYGOZXPXQIXORYHXTABCH
    IXOXOBKZXRUBUCZAKZSYIUDMZEMZYKYLLUEMZFMZUFZYGGYJHYLEMZHYNFMZUFZYBYDYFHYCEUG
    HYEFUGUHYKHOZYPYJYQYOUFYSYTYJYMYQYOYKHYLEUIUJYTYJYOYRYQYKHYNFUIUKULYIIOZYJY
    BYQYRYDYFYIIXRUBUMUUAYLYCHEYIISUDUNZUOUUAYNYEHFUUAYLYCLUEUUBUPUOUQJURXSXOIN
    USRZLUSRZUTXRUSRZUUDUTNXRUBUCZLLUBUCZUTXSXOVAUUCUUDVBVCVDUUEUUDSVEVFVGZVCVD
    UUFUUGNXRVBUUHVHVILVCVJVDNLXRLVKVLVMVNVOXPXQXTYGYFOZHLEMZHNFMZOZYBVPZIXROZV
    QZUUIXPXQUTXTUULUUMUUIUUNUUMUUIUULYBYDYFVRVSUULUUNUTYGYDYFUUNYGYDOZUULUUNYB
    UUPUUNYBXRXRUBUCXRUUHVJIXRXRUBUMVTYBYDYFWAWBVSUUNYDYFOUULUUNYDUUJYFUUKUUNYC
    LHEUUNYCSXRUDMZLIXRSUDUNZSWCVFWDZWEUOUUNYENHFUUNYEUUQLUEMZNUUNYCUUQLUEUURUP
    UUTLLUEMNUUQLLUEUUSWFLWGWHWIWEUOWJWKULWLXNUULDHXOXKHOXLUUJXMUUKXKHLEUIXKHNF
    UIWJWMXTIUSRZXRIUBUCZILUBUCZUAZUUOUUEUUDXTUVDWNUUHVCXRLIWOWPUVAUVBUUOUVCUVA
    UVBUTZXRIWQUCZXRIOZVQZUUOUVAUVBUVHUUEUVAUVBUVHWNUUHXRIWRWSWTUVEUVFUUMUVGUUN
    UVAUVFUUMWNZUVBUUEUVAUVIUUHXRIXAWSXBUVGUUNWNUVEXRIXCXDXEXFXGXHXIXJUL $.
    $( [2-Sep-2009] $)

  $( Lemma for ~ phtpyco . $)
  phtpycolem3 $p |- ( ( J e. Top /\ K e. ( ( II tX II ) Cn J ) ) ->
          ( M |` ( ( 0 [,] 1 ) X. ( 0 [,] ( 1 / 2 ) ) ) ) e. ( ( subSp `
      <. ( ( 0 [,] 1 ) X. ( 0 [,] ( 1 / 2 ) ) ) , ( II tX II ) >. ) Cn J ) ) $=
    ( vr vs vt vu vv vw ctop wcel cii ctx co ccn wa cc0 c1 cicc c2 cdiv cxp
    cres cv wceq copab cfv cmul cop copab2 ccom csubsp cuni wf wfn iitop eqid
    txtop mp2an iiuni eqcomi txuni cnf mp3an1 ffn iihalf1 anim2i oprex opelxp
    sylibr id visset fvopab4 adantr opreq2 adantl opeq12d eqeq2d pm5.32i
    oprabbii wral wb wss cle wbr cmin cif ifex fnoprab2 ssid cr 0re 1re pm3.2i
    2re 2ne0 rereccli leidi halflt1 ltleii iccss mp3an ssxp fnssres fvex
    eqfnfv c1st c2nd elxp6 phtpycolem1 oprvres opeq1 fveq2d opeq2d oprabval2
    df-opr syl6eqr 3eqtr4d 3eqtr3g fveq2 eqeq12d mpbird sylbi rgen mpbir2an
    oprabco 3syl w3a sseqtr4i stoig3 a1i pm3.26 3jca cabs copn txsubsp subspid
    ax-mp cme cnmet metres cc cdm iccssre axresscn sstri cnmetba metssba2
    df-ii metsstop resabs1 fveq2i eqtr3i opreq12i eqtri uniopn2 2txcn opntop
    cid df-id equcom opabbii reseq1 resopab eqtr2i idcn eqeltri ccncf resopab2
    2cn mulc1cncf wi rescncf eqeltrri 3pm3.2i eqeltrd cncffvrn cncfmet eleqtri
    syl3anc pm3.27 cnco sylan32c ) DOPZEQQRSZDTSPZUAZGUBUCUDSZUBUCUEUFSZUDSZUGZ
    UHZEIUIZUWMPZJUIZUWOPZUAZKUIZUWRLUIZUWMPMUIZUXDUJZUALMUKZULZUWTUXDUWOPUXEUE
    UXDUMSZUJZUALMUKZULZUNZUJZUAZIJKUOZUPZUWPUWJUNUQULZDTSZUWLUWMUWMUGZDURZEUSZ
    EUXTUTUWQUXQUJUWJOPZUWIUWKUYBQOPZUYDUYCVAVAQQUWJUWJVBZVCVDZEUWJDUXTUYAUWJUR
    ZUXTUYDUYDUYGUXTUJVAVAQQUWJUWMUWMUYEQURUWMVEVFZUYHVGVDZVFUYAVBVHVIUXTUYAEVJ
    IJKLUWMUWOUWRUEUWTUMSZUNZUXTUXPUWQEUXBUWSUYJUWMPZUAUYKUXTPUXAUYLUWSUWTVKVLU
    WRUYJUWMUWMUEUWTUMVMZVNVOUXOUXBUXCUYKUJZUAIJKUXBUXNUYNUXBUXMUYKUXCUXBUXHUWR
    UXLUYJUWSUXHUWRUJUXALMUWRUXDUWRUWMUXGUXDUWRUJVPUXGVBIVQVRVSUXAUXLUYJUJUWSLM
    UWTUXIUYJUWOUXKUXDUWTUEUMVTUXKVBZUYMVRWAWBWCWDWEUWQUXBUXDUYKEULZUJUAIJLUOZU
    JZUWPUWPUJZUXEUWQULZUXEUYQULZUJZMUWPWFZUWQUWPUTZUYQUWPUTUYRUYSVUCUAWGGUXTUT
    UWPUXTWHZVUDABCUWMUWMBUIZUWNWIWJZAUIZUEVUFUMSZESZVUHVUIUCWKSZFSZWLGVUGVUJVU
    LVUHVUIEVMVUHVUKFVMWMHWNUWMUWMWHZUWOUWMWHZVUEUWMWOZUBWPPZUCWPPZUAVUPUWNWPPZ
    UAUBUBWIWJZUWNUCWIWJZUAVUNVUPVUQWQWRWSVUPVURWQUEWTXAXBZWSVUSVUTUBWQXCUWNUCV
    VAWRXDXEWSUBUCUBUWNXFXGZUWMUWMUWOUWMXHVDZUXTUWPGXIVDIJLUWMUWOUYPUYQUYKEXJUY
    QVBZWNMUWPUWPUWQUYQXKVDUWPVBVUBMUWPUXEUWPPUXEUXEXLULZUXEXMULZUNZUJZVVEUWMPV
    VFUWOPUAZUAZVUBUXEUWMUWOXNVVJVUBVVGUWQULZVVGUYQULZUJZVVIVVMVVHVVIVVEVVFUWQS
    ZVVEVVFUYQSZVVKVVLVVIVVEVVFGSVVEUEVVFUMSZESZVVNVVOABCEFGVVEVVFHXOVVEVVFUWMU
    WOGXPVVIVVOVVEVVPUNZEULZVVQIJLVVEVVFUWMUWOUYPVVSUYQVVEUYJUNZEULVVREXJUWRVVE
    UJUYKVVTEUWRVVEUYJXQXRUWTVVFUJZVVTVVREVWAUYJVVPVVEUWTVVFUEUMVTXSXRVVDXTVVEV
    VPEYAYBYCVVEVVFUWQYAVVEVVFUYQYAYDWAVVHVUBVVMWGVVIVVHUYTVVKVUAVVLUXEVVGUWQYE
    UXEVVGUYQYEYFVSYGYHYIYJYKYLUXROPZUYCUWIYMUXPUXRUWJTSPZUWKUXQUXSPUWLUWLVWBUY
    CUWIVWBUWLUYCUWPUYGWHVWBUYFUWPUXTUYGVVCUYIYNUWPUWJYOVDYPUYCUWLUYFYPUWIUWKYQ
    YRUYDYSWKUPZUWOUWOUGZUHZYTULZOPZUAZUYDUYDUAZUXGQQTSZPZUXKVWGQTSZPZUAZVWCUWL
    IJKQVWGUXRUXGUXKUXPQQUWJUWMUWOUXRUWMQUNUQULZUWOQUNUQULZRSZQVWGRSVWJVUMVUNUA
    UXRVWRUJUYDUYDVAVAWSZVUMVUNVUOVVBWSUWMUWOQQUWMUWMUYHUYHUUAVDVWPQVWQVWGRUYDV
    WPQUJVAQUWMUYHUUBUUCVWDUXTUHZVWEUHZYTULZVWQVWGVWTUUDPZVUNVXBVWQUJVWDUUDPZVX
    CVWDVWDVBZUUEZVWDUWMUUFUUCVVBQVWTUWMUWOVXDUWMUUGWHZUWMVWTUUHUUHUJVXFUWMWPUU
    GVUPVUQUWMWPWHWQWRUBUCUUIVDUUJUUKZVWDUUGUWMVWDVXEUULZUUMVDUUNUUOVDVXAVWFYTV
    WEUXTWHZVXAVWFUJVUNVUNVXJVVBVVBUWOUWMUWOUWMXHVDVWDVWEUXTUUPUUCUUQUURUUSUUTU
    YEUYHVWGURZUWOVWFUUDPZVXKUWOUJVXDVXLVXFVWDUWOUUFUUCZVWFVWGUWOVXDUWOUUGWHZUW
    OVWFUUHUUHUJVXFUWOWPUUGVUPVURUWOWPWHWQVVAUBUWNUUIVDUUJUUKZVWDUUGUWOVXIUUMVD
    VWGVBZUVAUUCVFUXPVBUVBVWIUWLUYDVWHVAVXLVWHVXMVWFVWGVXPUVCUUCWSYPVWJUWLVWSYP
    VWOUWLVWLVWNUXGUVDUWMUHZVWKVXQUXFLMUKZUWMUHZUXGUVDVXRUJVXQVXSUJUVDUXDUXEUJZ
    LMUKVXRLMUVEVXTUXFLMLMUVFUVGUUTUVDVXRUWMUVHUUCUXFLMUWMUVIUVJUYDVXQVWKPVAQUW
    MUYHUVKUUCUVLUXKUWOUWMUVMSZVWMUXKUWOUUGUVMSZPZUXKVYAPZUXDUUGPUXJUALMUKZUWOU
    HZUXKVYBVXNVYFUXKUJVXOUXJLMUWOUUGUVNUUCVYEUUGUUGUVMSPZVYFVYBPZUEUUGPVYGUVOL
    MUEVYEVYEVBUVPUUCUUGUUGWHZVYIVXNVYGVYHUVQUUGWOZVYJVXOUUGUUGUWOVYEUVRXGUUCUV
    SVXNVYIVXGYMNUIZUXKULZUWMPZNUWOWFVYCVYDUVQVXNVYIVXGVXOVYJVXHUVTVYMNUWOVYKUW
    OPVYLUEVYKUMSZUWMLMVYKUXIVYNUWOUXKUXDVYKUEUMVTUYOUEVYKUMVMVRVYKVKUWAYINUWOU
    UGUWMUXKUWBVDUUCVXNVXGVYAVWMUJVXOVXHUWOUWMVWFVWTVWGQVWFVBVWTVBVXPUUNUWCVDUW
    DWSYPUWEUWIUWKUWFUXPEUXRUWJDUWGUWHUWA $.
    $( [2-Sep-2009] $)

  $( Lemma for ~ phtpyco . $)
  phtpycolem4 $p |- ( ( J e. Top /\ L e. ( ( II tX II ) Cn J ) /\
                  A. w e. ( 0 [,] 1 ) ( w K 1 ) = ( w L 0 ) ) ->
              ( M |` ( ( 0 [,] 1 ) X. ( ( 1 / 2 ) [,] 1 ) ) ) e. ( ( subSp `
      <. ( ( 0 [,] 1 ) X. ( ( 1 / 2 ) [,] 1 ) ) , ( II tX II ) >. ) Cn J ) ) $=
    ( vr vs vt vu vv ctop wcel cii ctx co ccn cv c1 cc0 wceq cicc wral w3a c2
    cdiv cxp cres wa copab cfv cmul cmin cop copab2 ccom csubsp wfn wb wss cle
    wbr cif oprex ifex fnoprab2 ssid cr 0re 1re pm3.2i 2re 2ne0 rereccli
    halfgt0 ltleii leidi iccss mp3an ssxp mp2an fnssres fvex eqid eqfnfv eqidd
    c1st c2nd phtpycolem2 3expb oprvres adantl opeq1 fveq2d opreq2 opreq1d
    opeq2d oprabval2 df-opr syl6eqr 3eqtr4d 3eqtr3g adantrl fveq2 eqeq12d
    ad2antrl mpbird elxp6 sylan2b r19.21aiva sylanbrc iihalf2 anim2i opelxp
    sylibr id visset fvopab4 adantr opeq12d eqeq2d pm5.32i oprabbii oprabco
    sylan9eqr iitop txtop cuni wf iiuni eqcomi txuni cnf ffn syl mp3an1 sylan
    3impa sseqtr4i stoig3 a1i 3simp1 3jca cabs copn txsubsp subspid ax-mp cme
    cnmet metres cc cdm iccssre axresscn sstri cnmetba metssba2 df-ii metsstop
    resabs1 fveq2i eqtr3i opreq12i eqtri uniopn2 2txcn opntop cid df-id equcom
    opabbii reseq1 resopab eqtr2i idcn eqeltri ccncf resopab2 crn fnopab2 2cn
    mulcl mpan fopab frn fnco wfun fvco fnfun dmopab2 eleq2i biimpri syl3anc
    opreq1 3eqtrd eqtr4d rgen mpbir2an 3pm3.2i mulc1cncf ax1cn sub1cncf cncfco
    eqeltrri wi rescncf eqeltrd cncffvrn cncfmet eleqtri 3simp2 cnco sylan32c
    ) EOPZGQQRSZETSPZDUAZUBFSUYAUCGSUDDUCUBUESZUFZUGZHUYBUBUHUISZUBUESZUJZUKZGJ
    UAZUYBPZKUAZUYFPZULZLUAZUYIMUAZUYBPNUAZUYOUDZULMNUMZUNZUYKUYOUYFPUYPUHUYOUO
    SZUBUPSZUDZULMNUMZUNZUQZUDZULZJKLURZUSZUYGUXSUQUTUNZETSZUXRUXTUYCUYHVUIUDZG
    UYBUYBUJZVAZUYCVULUXRUXTULUYCVUNUYHUYMUYOUYIUHUYKUOSZUBUPSZUQZGUNZUDULJKMUR
    ZVUIUYHVUSUDZUYGUYGUDZUYPUYHUNZUYPVUSUNZUDZNUYGUFZUYCUYHUYGVAZVUSUYGVAVUTVV
    AVVEULVBHVUMVAUYGVUMVCZVVFABCUYBUYBBUAZUYEVDVEZAUAZUHVVHUOSZFSZVVJVVKUBUPSZ
    GSZVFHVVIVVLVVNVVJVVKFVGVVJVVMGVGVHIVIUYBUYBVCZUYFUYBVCZVVGUYBVJZUCVKPZUBVK
    PZULUYEVKPZVVSULUCUYEVDVEZUBUBVDVEZULVVPVVRVVSVLVMVNVVTVVSUHVOVPVQZVMVNVWAV
    WBUCUYEVLVWCVRVSUBVMVTVNUCUBUYEUBWAWBZUYBUYBUYFUYBWCWDZVUMUYGHWEWDJKMUYBUYF
    VURVUSVUQGWFVUSWGZVINUYGUYGUYHVUSWHWDUYCUYGWIUYCVVDNUYGUYCUYPUYPWJUNZUYPWKU
    NZUQZUDZVWGUYBPZVWHUYFPZULZULZVVDUYPUYGPUYCVWNULVVDVWIUYHUNZVWIVUSUNZUDZUYC
    VWMVWQVWJUYCVWMULZVWGVWHUYHSZVWGVWHVUSSZVWOVWPVWRVWGVWHHSZVWGUHVWHUOSZUBUPS
    ZGSZVWSVWTUYCVWKVWLVXAVXDUDABCDFGHVWGVWHIWLWMVWMVWSVXAUDUYCVWGVWHUYBUYFHWNW
    OVWMVWTVXDUDUYCVWMVWTVWGVXCUQZGUNZVXDJKMVWGVWHUYBUYFVURVXFVUSVWGVUPUQZGUNVX
    EGWFUYIVWGUDVUQVXGGUYIVWGVUPWPWQUYKVWHUDZVXGVXEGVXHVUPVXCVWGVXHVUOVXBUBUPUY
    KVWHUHUOWRWSWTWQVWFXAVWGVXCGXBXCWOXDVWGVWHUYHXBVWGVWHVUSXBXEXFVWJVVDVWQVBUY
    CVWMVWJVVBVWOVVCVWPUYPVWIUYHXGUYPVWIVUSXGXHXIXJUYPUYBUYFXKXLXMXNJKLMUYBUYFV
    UQVUMVUHVUSGUYMUYJVUPUYBPZULVUQVUMPUYLVXIUYJUYKXOXPUYIVUPUYBUYBVUOUBUPVGZXQ
    XRVUGUYMUYNVUQUDZULJKLUYMVUFVXKUYMVUEVUQUYNUYMUYSUYIVUDVUPUYJUYSUYIUDUYLMNU
    YIUYOUYIUYBUYRUYOUYIUDXSUYRWGJXTYAYBUYLVUDVUPUDUYJMNUYKVUAVUPUYFVUCUYOUYKUD
    UYTVUOUBUPUYOUYKUHUOWRWSVUCWGZVXJYAWOYCYDYEYFVWFYGYHUXSOPZUXRUXTVUNQOPZVXNV
    XMYIYIQQUXSUXSWGZYJWDZVXMUXRUXTUGVUMEYKZGYLVUNGUXSEVUMVXQUXSYKZVUMVXNVXNVXR
    VUMUDYIYIQQUXSUYBUYBVXOQYKUYBYMYNZVXSYOWDZYNVXQWGYPVUMVXQGYQYRYSYTUUAVUJOPZ
    VXMUXRUGVUHVUJUXSTSPZUXTVUIVUKPUYDUYDVYAVXMUXRVYAUYDVXMUYGVXRVCVYAVXPUYGVUM
    VXRVWEVXTUUBUYGUXSUUCWDUUDVXMUYDVXPUUDUXRUXTUYCUUEUUFVXNUUGUPUSZUYFUYFUJZUK
    ZUUHUNZOPZULZVXNVXNULZUYRQQTSZPZVUCVYFQTSZPZULZVYBUYDJKLQVYFVUJUYRVUCVUHQQU
    XSUYBUYFVUJUYBQUQUTUNZUYFQUQUTUNZRSZQVYFRSVYIVVOVVPULVUJVYQUDVXNVXNYIYIVNZV
    VOVVPVVQVWDVNUYBUYFQQUYBUYBVXSVXSUUIWDVYOQVYPVYFRVXNVYOQUDYIQUYBVXSUUJUUKVY
    CVUMUKZVYDUKZUUHUNZVYPVYFVYSUULPZVVPWUAVYPUDVYCUULPZWUBVYCVYCWGZUUMZVYCUYBU
    UNUUKVWDQVYSUYBUYFWUCUYBUUOVCZUYBVYSUUPUUPUDWUEUYBVKUUOVVRVVSUYBVKVCVLVMUCU
    BUUQWDUURUUSZVYCUUOUYBVYCWUDUUTZUVAWDUVBUVCWDVYTVYEUUHVYDVUMVCZVYTVYEUDVVPV
    VPWUIVWDVWDUYFUYBUYFUYBWCWDVYCVYDVUMUVDUUKUVEUVFUVGUVHVXOVXSVYFYKZUYFVYEUUL
    PZWUJUYFUDWUCWUKWUEVYCUYFUUNUUKZVYEVYFUYFWUCUYFUUOVCZUYFVYEUUPUUPUDWUEUYFVK
    UUOVVTVVSUYFVKVCVWCVMUYEUBUUQWDUURUUSZVYCUUOUYFWUHUVAWDVYFWGZUVIUUKYNVUHWGU
    VJVYHUYDVXNVYGYIWUKVYGWULVYEVYFWUOUVKUUKVNUUDVYIUYDVYRUUDVYNUYDVYKVYMUYRUVL
    UYBUKZVYJWUPUYQMNUMZUYBUKZUYRUVLWUQUDWUPWURUDUVLUYOUYPUDZMNUMWUQMNUVMWUSUYQ
    MNMNUVNUVOUVHUVLWUQUYBUVPUUKUYQMNUYBUVQUVRVXNWUPVYJPYIQUYBVXSUVSUUKUVTVUCUY
    FUYBUWASZVYLVUCUYFUUOUWASZPZVUCWUTPZUYOUUOPVUBULMNUMZUYFUKZVUCWVAWUMWVEVUCU
    DWUNVUBMNUYFUUOUWBUUKWVDUUOUUOUWASZPZWVEWVAPZUYAUUOPCUAUYAUBUPSZUDULDCUMZVV
    JUUOPZVVHUHVVJUOSZUDULABUMZUSZWVDWVFWVNWVDUDZUUOUUOUDZUYNWVNUNZUYNWVDUNZUDZ
    LUUOUFZWVNUUOVAZWVDUUOVAWVOWVPWVTULVBWVJUUOVAZWVMUUOVAZWVMUWCUUOVCZWWADCUUO
    WVIWVJUYAUBUPVGWVJWGZUWDZABUUOWVLWVMUHVVJUOVGZWVMWGZUWDZUUOUUOWVMYLWWDABUUO
    UUOWVLWVMWWHUHUUOPZWVKWVLUUOPUWEUHVVJUWFUWGUWHUUOUUOWVMUWIUUKUUOUUOWVJWVMUW
    JWBMNUUOVUAWVDUYTUBUPVGWVDWGZUWDLUUOUUOWVNWVDWHWDUUOWGWVSLUUOUYNUUOPZWVQUHU
    YNUOSZUBUPSZWVRWWLWVQUYNWVMUNZWVJUNZWWMWVJUNZWWNWVJUWKZWVMUWKZUYNWVMUUPZPZW
    VQWWPUDWWLUYNWVJWVMUWLWWRWWLWWBWWRWWFUUOWVJUWMUUKUUDWWSWWLWWCWWSWWIUUOWVMUW
    MUUKUUDWXAWWLWWTUUOUYNABUUOWVLWVMWWGWWHUWNUWOUWPUWQWWLWWOWWMWVJABUYNWVLWWMU
    UOWVMVVJUYNUHUOWRWWHUHUYNUOVGYAWQWWLWWMUUOPZWWQWWNUDWWJWWLWXBUWEUHUYNUWFUWG
    DCWWMWVIWWNUUOWVJUYAWWMUBUPUWRWWEWWMUBUPVGZYAYRUWSMNUYNVUAWWNUUOWVDUYOUYNUD
    UYTWWMUBUPUYOUYNUHUOWRWSWWKWXCYAUWTUXAUXBUUOUUOVCZWXDWXDUGWVMWVFPZWVJWVFPZU
    LWVNWVFPWXDWXDWXDUUOVJZWXGWXGUXCWXEWXFWWJWXEUWEABUHWVMWWHUXDUUKUBUUOPWXFUXE
    DCUBWVJWWEUXFUUKVNUUOUUOUUOWVMWVJUXGWDUXHWXDWXDWUMWVGWVHUXIWXGWXGWUNUUOUUOU
    YFWVDUXJWBUUKUXHWUMWXDWUFUGUYAVUCUNZUYBPZDUYFUFWVBWVCUXIWUMWXDWUFWUNWXGWUGU
    XCWXIDUYFUYAUYFPWXHUHUYAUOSZUBUPSZUYBMNUYAVUAWXKUYFVUCUYOUYAUDUYTWXJUBUPUYO
    UYAUHUOWRWSVXLWXJUBUPVGYAUYAXOUXKUXADUYFUUOUYBVUCUXLWDUUKWUMWUFWUTVYLUDWUNW
    UGUYFUYBVYEVYSVYFQVYEWGVYSWGWUOUVBUXMWDUXNVNUUDUWQUXRUXTUYCUXOVUHGVUJUXSEUX
    PUXQUXK $.
    $( [2-Sep-2009] $)

  $( Lemma for ~ phtpyco . $)
  phtpycolem5 $p |- ( ( ( J e. Top /\ A. w e. ( 0 [,] 1 ) ( w K 1 ) = ( w L 0 )
          ) /\ ( K e. ( ( II tX II ) Cn J ) /\ L e. ( ( II tX II ) Cn J ) ) )
                                    -> M e. ( ( II tX II ) Cn J ) ) $=
    ( vv cii ctx co ctop wcel wa cc0 c1 cicc c2 cdiv cxp ccld cfv cun wceq w3a
    cuni wf cres cop csubsp ccn cv wral iitop eqid iiuni eqcomi txuni mp2an
    paste simpll txtop jctil pm3.2i topcld ax-mp cioo crn ctg cr wss retop 0re
    1re iccssre 2re 2ne0 rereccli clint3 cle wbr leidi halflt1 ltleii iccss
    mp3an uniretop subspcld dfii2 fveq2i eleqtrri txcld halfgt0 xpundi 3pm3.2i
    wb elicc2 mpbir iccsplit xpeq2i eqtr3i a1i wfn c1st c2nd fveq2 df-opr
    syl6eqr eleq1d wo cmul phtpycolem1 adantll ffvelrn syl5eqel oprex opelxp
    sylan2br iihalf1 sylanr2 cnf mp3an1 sylan adantlrr adantllr anassrs
    eqeltrd cmin phtpycolem2 3expa adantlll iihalf2 adantlrl jaodan eleq2i
    elun bitri sylan2b anasss syl5cbir impr ancom2s elxp6 r19.21aiva cif ifex
    fnoprab2 ffnfv sylibr phtpycolem3 ad2ant2r phtpycolem4 an1rs adantrl 3jca
    syl3anc ) KKLMZNOZENOZPQRSMZQRTUAMZSMZUBZUUSUCUDZOZUVBUVCRSMZUBZUVFOZUVEUVI
    UEZUVBUVBUBZUFZUGZUVLEUHZHUIZHUVEUJUVEUUSUKULUDEUMMOZHUVIUJUVIUUSUKULUDEUMM
    OZUGHUUSEUMMZOUVADUNZRFMUVTQGMUFDUVBUOZPZFUVSOZGUVSOZPZPZUVEUVIHUUSEUVLUVOU
    USUHZUVLKNOZUWHUWGUVLUFUPUPKKUUSUVBUVBUUSUQZKUHUVBURUSZUWJUTVAUSZUVOUQZVBUW
    FUVAUUTUVAUWAUWEVCUWHUWHUUTUPUPKKUUSUWIVDVAZVEUVNUWFUVGUVJUVMUWHUWHPZUVBKUC
    UDZOZUVDUWOOZPUVGUWHUWHUPUPVFZUWPUWQUWHUWPUPKUVBUWJVGVHZUVDUVBVIVJVKUDZUKUL
    UDZUCUDZUWOUWTNOZUVBVLVMZPZUVDUWTUCUDZOZUVDUVBVMZPUVDUXBOUXCUXDVNQVLOZRVLOZ
    UXDVOVPQRVQVAVFZUXGUXHUXIUVCVLOZUXGVOTVRVSVTZQUVCWAVAUXIUXJPZUXIUXLPQQWBWCZ
    UVCRWBWCZPUXHUXIUXJVOVPVFZUXIUXLVOUXMVFUXOUXPQVOWDUVCRUXMVPWEWFZVFQRQUVCWGW
    HVFUVBUVDUWTVLUWTUHVLWIUSZWJVAKUXAUCWKWLZWMVFUVBUVDKKUUSUWIWNVAUWNUWPUVHUWO
    OZPUVJUWRUWPUYAUWSUVHUXBUWOUXEUVHUXFOZUVHUVBVMZPUVHUXBOUXKUYBUYCUXLUXJUYBUX
    MVPUVCRWAVAUXNUXLUXJPQUVCWBWCZRRWBWCZPUYCUXQUXLUXJUXMVPVFUYDUYEQUVCVOUXMWOW
    FZRVPWDVFQRUVCRWGWHVFUVBUVHUWTVLUXSWJVAUXTWMVFUVBUVHKKUUSUWIWNVAUVBUVDUVHUE
    ZUBUVKUVLUVBUVDUVHWPUYGUVBUVBUVBUYGUXIUXJUVCUVBOZUVBUYGUFVOVPUYHUXLUYDUXPUG
    ZUXLUYDUXPUXMUYFUXRWQUXIUXJUYHUYIWRVOVPQRUVCWSVAWTQRUVCXAWHZUSXBXCWQXDUWFUV
    PUVQUVRUWFHUVLXEZJUNZHUDZUVOOZJUVLUOZPUVPUWFUYOUYKUWFUYNJUVLUWFUYLUYLXFUDZU
    YLXGUDZUKZUFZUYPUVBOZUYQUVBOZPZPUYNUYLUVLOUWFVUBUYSUYNUWFVUBUYSUYNUYSUYNUYP
    UYQHMZUVOOZUWFVUBPUYSUYMVUCUVOUYSUYMUYRHUDVUCUYLUYRHXHUYPUYQHXIXJXKUWFUYTVU
    AVUDUWFUYTPZUYQUVDOZUYQUVHOZXLZVUDVUAVUEVUFVUDVUGVUEVUFPVUCUYPTUYQXMMZFMZUV
    OUYTVUFVUCVUJUFUWFABCFGHUYPUYQIXNXOUWFUYTVUFVUJUVOOZUVAUWEUYTVUFPZVUKUWAUVA
    UWCVULVUKUWDUVLUVOFUIZVULVUKUVAUWCPVUMUYTVUIUVBOZVUKVUFVUMUYPVUIUKZUVLOZVUK
    UYTVUNPVUMVUPPVUOFUDUVOVUJUVLUVOVUOFXPUYPVUIFXIXQUYPVUIUVBUVBTUYQXMXRXSXTUY
    QYAYBUUTUVAUWCVUMUWMFUUSEUVLUVOUWKUWLYCYDYEYFYGYHYIVUEVUGPVUCUYPVUIRYJMZGMZ
    UVOUWBUYTVUGVUCVURUFZUWEUWAUYTVUGVUSUVAUWAUYTVUGVUSABCDFGHUYPUYQIYKYLYMYGUW
    FUYTVUGVURUVOOZUVAUWEUYTVUGPZVUTUWAUVAUWDVVAVUTUWCUVLUVOGUIZVVAVUTUVAUWDPVV
    BUYTVUQUVBOZVUTVUGVVBUYPVUQUKZUVLOZVUTUYTVVCPVVBVVEPVVDGUDUVOVURUVLUVOVVDGX
    PUYPVUQGXIXQUYPVUQUVBUVBVUIRYJXRXSXTUYQYNYBUUTUVAUWDVVBUWMGUUSEUVLUVOUWKUWL
    YCYDYEYOYGYHYIYPVUAUYQUYGOVUHUVBUYGUYQUYJYQUYQUVDUVHYRYSYTUUAUUBUUCUUDUYLUV
    BUVBUUEYTUUFABCUVBUVBBUNZUVCWBWCZAUNZTVVFXMMZFMZVVHVVIRYJMZGMZUUGHVVGVVJVVL
    VVHVVIFXRVVHVVKGXRUUHIUUIVEJUVLUVOHUUJUUKUVAUWCUVQUWAUWDABCEFGHIUULUUMUWBUW
    DUVRUWCUVAUWDUWAUVRUVAUWDUWAUVRABCDEFGHIUUNYLUUOUUPUUQUUR $.
    $( [2-Sep-2009] $)

  $( Concatenate two path homotopies. $)
  phtpyco $p |- ( ( J e. Top /\ ( F e. ( II Cn J ) /\ G e. ( II Cn J ) /\
    H e. ( II Cn J ) ) /\ ( ( ( F ` 0 ) = ( G ` 0 ) /\ ( F ` 1 ) = ( G ` 1 ) )
            /\ ( ( G ` 0 ) = ( H ` 0 ) /\ ( G ` 1 ) = ( H ` 1 ) ) ) ) ->
              ( ( K e. ( F ( PHtpy ` J ) G ) /\ L e. ( G ( PHtpy ` J ) H ) ) ->
                                        M e. ( F ( PHtpy ` J ) H ) ) ) $=
    ( vr vs vt vw ctop wcel cii ccn co w3a cc0 cfv wceq c1 wa ctx cv cicc wral
    cphtpy phtpycolem5 3simp1 opreq1 fveq2 eqeq12d anbi12d opreq2 eqeq1d
    rcla4v anim12d eqtr3 ad2ant2lr ad2ant2r syl6com r19.21aiv ad2ant2l anim12i
    simpll simprl jca adantl sylanc c2 cmul cdiv cr cle wbr 0re leidi 2re 2ne0
    rereccli halfgt0 ltleii 3pm3.2i wb elicc2 mp2an mpbir phtpycolem1 mpan2
    rcla4cva syl adantll adantlr 2cn mul01i opreq2i syl5eq eqtrd cmin 1re
    halflt1 phtpycolem2 mp3an3 sylan simplr mulid1i opreq1i ax1cn 1p1e2
    subaddrii eqtri wo lt01 lbicc2 mp3an mpan iihalf1 sylan2 iihalf2 mp3an2
    ad2antrr 3eqtr4d jaodan cun iccsplit eleq2i elun bitri sylan2b ubicc2
    simprr jca31 r19.21aiva 3ad2antl3 ex isphtpy id 3adant3r3 3adant3 3adant1
    3adant3r1 3adant3r2 eqtr an4s 3ad2ant3 3imtr4d ) GPQZDRGSTZQZEUULQZFUULQZUA
    ZUBDUCZUBEUCZUDZUEDUCZUEEUCZUDZUFZUURUBFUCZUDZUVAUEFUCZUDZUFZUFZUAZHRRUGTGS
    TZQZLUHZUBHTZUVMDUCZUDZUVMUEHTZUVMEUCZUDZUFZUBUVMHTZUUQUDZUEUVMHTZUUTUDZUFZ
    UFZLUBUEUITZUJZUFZIUVKQZMUHZUBITZUWKEUCZUDZUWKUEITZUWKFUCZUDZUFZUBUWKITZUUR
    UDZUEUWKITZUVAUDZUFZUFZMUWGUJZUFZUFZJUVKQZNUHZUBJTZUXIDUCZUDZUXIUEJTZUXIFUC
    ZUDZUFUBUXIJTZUUQUDZUEUXIJTZUUTUDZUFZUFZNUWGUJZUFZHDEGUKUCZTQZIEFUYDTQZUFJD
    FUYDTQZUVJUXGUYCUVJUXGUFZUXHUYBUUKOUHZUEHTZUYIUBITZUDZOUWGUJZUFUVLUWJUFZUXH
    UYHABCOGHIJKULUVJUUKUXGUYMUUKUUPUVIUMUWHUXEUYMUVLUWJUWHUXEUFZUYLOUWGUYIUWGQ
    ZUYOUYIUBHTZUYIDUCZUDZUYJUYIEUCZUDZUFZUBUYIHTZUUQUDZUEUYIHTZUUTUDZUFZUFZUYK
    UYTUDZUYIUEITZUYIFUCZUDZUFZUBUYIITZUURUDZUEUYIITZUVAUDZUFZUFZUFUYLUYPUWHVUH
    UXEVUSUWFVUHLUYIUWGUVMUYIUDZUVTVUBUWEVUGVUTUVPUYSUVSVUAVUTUVNUYQUVOUYRUVMUY
    IUBHUNUVMUYIDUOUPVUTUVQUYJUVRUYTUVMUYIUEHUNUVMUYIEUOUPUQVUTUWBVUDUWDVUFVUTU
    WAVUCUUQUVMUYIUBHURUSVUTUWCVUEUUTUVMUYIUEHURUSUQUQUTUXDVUSMUYIUWGUWKUYIUDZU
    WRVUMUXCVURVVAUWNVUIUWQVULVVAUWLUYKUWMUYTUWKUYIUBIUNUWKUYIEUOUPVVAUWOVUJUWP
    VUKUWKUYIUEIUNUWKUYIFUOUPUQVVAUWTVUOUXBVUQVVAUWSVUNUURUWKUYIUBIURUSVVAUXAVU
    PUVAUWKUYIUEIURUSUQUQUTVAVUBVUMUYLVUGVURVUAVUIUYLUYSVULUYJUYKUYTVBVCVDVEVFV
    GZVHUXGUYNUVJUXGUVLUWJUVLUWHUXFVIUWIUWJUXEVJVKVLVMUVIUUKUXGUYBUUPUVIUXGUFZU
    YANUWGVVCUXIUWGQZUFZUXLUXOUXTVVEUXJUXIVNUBVOTZHTZUXKVVDUXJVVGUDZVVCVVDUBUBU
    EVNVPTZUITZQZVVHVVKUBVQQZUBUBVRVSZUBVVIVRVSZUAZVVLVVMVVNVTUBVTWAUBVVIVTVNWB
    WCWDZWEWFZWGVVLVVIVQQZVVKVVOWHVTVVPUBVVIUBWIWJWKABCHIJUXIUBKWLWMVLVVEUXIUBH
    TZUXKVVGUXGVVDVVSUXKUDZUVIUWIVVDVVTUXFUWHVVDVVTUVLUWHVVDUFVVTUXIUEHTZUXIEUC
    ZUDZUFZUBUXIHTZUUQUDZUEUXIHTZUUTUDZUFZUFZVVTUWFVWJLUXIUWGUVMUXIUDZUVTVWDUWE
    VWIVWKUVPVVTUVSVWCVWKUVNVVSUVOUXKUVMUXIUBHUNUVMUXIDUOUPVWKUVQVWAUVRVWBUVMUX
    IUEHUNUVMUXIEUOUPUQVWKUWBVWFUWDVWHVWKUWAVWEUUQUVMUXIUBHURUSVWKUWCVWGUUTUVMU
    XIUEHURUSUQUQWNVVTVWCVWIVIWOWPWQWPVVFUBUXIHVNWRWSWTXAXBVVEUXMUXIVNUEVOTZUEX
    CTZITZUXNUYMVVDUXMVWNUDZVVCUYMVVDUEVVIUEUITZQZVWOVWQUEVQQZVVIUEVRVSZUEUEVRV
    SZUAZVWRVWSVWTXDVVIUEVVPXDXEWFZUEXDWAWGVVRVWRVWQVXAWHVVPXDVVIUEUEWIWJWKABCO
    HIJUXIUEKXFXGUXGUYMUVIVVBVLZXHVVEUXIUEITZUXNVWNUXGVVDVXDUXNUDZUVIUXFVVDVXEU
    WIUXEVVDVXEUWJUXEVVDUFUXIUBITZVWBUDZVXEUFZUBUXIITZUURUDZUEUXIITZUVAUDZUFZUF
    ZVXEUXDVXNMUXIUWGUWKUXIUDZUWRVXHUXCVXMVXOUWNVXGUWQVXEVXOUWLVXFUWMVWBUWKUXIU
    BIUNUWKUXIEUOUPVXOUWOVXDUWPUXNUWKUXIUEIUNUWKUXIFUOUPUQVXOUWTVXJUXBVXLVXOUWS
    VXIUURUWKUXIUBIURUSVXOUXAVXKUVAUWKUXIUEIURUSUQUQWNVXGVXEVXMXIWOWPWPWPVWMUEU
    XIIVWMVNUEXCTUEVWLVNUEXCVNWRXJXKVNUEUEWRXLXLXMXNXOWTXAXBVVEUXQUXSVVCUXIVVJQ
    ZUXIVWPQZXPZUXQVVDVVCVXPUXQVXQVVCVXPUFZUXPUBVNUXIVOTZHTZUUQVXPUXPVYAUDZVVCU
    BUWGQZVXPVYBVVLVWRUBUEVRVSZVYCVTXDUBUEVTXDXQWFZUBUEXRXSZABCHIJUBUXIKWLXTVLU
    XGVXPVYAUUQUDZUVIUWIVXPVYGUXFUWHVXPVYGUVLUWHVXTUWGQZVYGVXPUWHVYHUFZVXTUBHTZ
    VXTDUCZUDZVXTUEHTZVXTEUCZUDZUFZVYGUEVXTHTZUUTUDZUFZUFZVYGUWFVYTLVXTUWGUVMVX
    TUDZUVTVYPUWEVYSWUAUVPVYLUVSVYOWUAUVNVYJUVOVYKUVMVXTUBHUNUVMVXTDUOUPWUAUVQV
    YMUVRVYNUVMVXTUEHUNUVMVXTEUOUPUQWUAUWBVYGUWDVYRWUAUWAVYAUUQUVMVXTUBHURUSWUA
    UWCVYQUUTUVMVXTUEHURUSUQUQWNZVYPVYGVYRVJWOUXIYAZYBWPWQWPXBVVCVXQUFZUBVXTUEX
    CTZITZUURUXPUUQUXGVXQWUFUURUDZUVIUXFVXQWUGUWIUXEVXQWUGUWJUXEWUEUWGQZWUGVXQU
    XEWUHUFZWUEUBITZWUEEUCZUDZWUEUEITZWUEFUCZUDZUFZWUGUEWUEITZUVAUDZUFZUFZWUGUX
    DWUTMWUEUWGUWKWUEUDZUWRWUPUXCWUSWVAUWNWULUWQWUOWVAUWLWUJUWMWUKUWKWUEUBIUNUW
    KWUEEUOUPWVAUWOWUMUWPWUNUWKWUEUEIUNUWKWUEFUOUPUQWVAUWTWUGUXBWURWVAUWSWUFUUR
    UWKWUEUBIURUSWVAUXAWUQUVAUWKWUEUEIURUSUQUQWNZWUPWUGWURVJWOUXIYCZYBWPWPWPUYM
    VXQUXPWUFUDZVVCUYMVYCVXQWVDVYFABCOHIJUBUXIKXFYDVXCXHUVIUUSUXGVXQUUSUVBUVHVI
    YEYFYGVVDUXIVVJVWPYHZQVXRUWGWVEUXIVVLVWRVVIUWGQZUWGWVEUDVTXDWVFVVRVVNVWSUAZ
    VVRVVNVWSVVPVVQVXBWGVVLVWRWVFWVGWHVTXDUBUEVVIWIWJWKUBUEVVIYIXSYJUXIVVJVWPYK
    YLZYMVVCVXRUXSVVDVVCVXPUXSVXQVXSUXRVYQUUTVXPUXRVYQUDZVVCUEUWGQZVXPWVIVVLVWR
    VYDWVJVTXDVYEUBUEYNXSZABCHIJUEUXIKWLXTVLUXGVXPVYRUVIUWIVXPVYRUXFUWHVXPVYRUV
    LUWHVYHVYRVXPVYIVYTVYRWUBVYPVYGVYRYOWOWUCYBWPWQWPXBWUDWUQUVAUXRUUTUXGVXQWUR
    UVIUXFVXQWURUWIUXEVXQWURUWJUXEWUHWURVXQWUIWUTWURWVBWUPWUGWURYOWOWVCYBWPWPWP
    UYMVXQUXRWUQUDZVVCUYMWVJVXQWVLWVKABCOHIJUEUXIKXFYDVXCXHUVIUVBUXGVXQUUSUVBUV
    HXIYEYFYGWVHYMVKYPYQYRVKYSUVJUYEUWIUYFUXFUUKUUMUUNUAZUVCUYEUWIWHUVJDEHGLYTU
    UKUUPWVMUVIUUKUUMUUNWVMUUOWVMUUAUUBUUCUUPUVIUVCUUKUUPUVCUVHVJUUDVMUUKUUNUUO
    UAZUVHUYFUXFWHUVJEFIGMYTUUKUUPWVNUVIUUKUUNUUOWVNUUMWVNUUAUUEUUCUUPUVIUVHUUK
    UUPUVCUVHYOUUDVMUQUUKUUMUUOUAZUUQUVDUDZUUTUVFUDZUFZUYGUYCWHUVJDFJGNYTUUKUUP
    WVOUVIUUKUUMUUOWVOUUNWVOUUAUUFUUCUVIUUKWVRUUPUUSUVEUVBUVGWVRUUSUVEUFWVPUVBU
    VGUFWVQUUQUURUVDUUGUUTUVAUVFUUGVHUUHUUIVMUUJ $.
    $( [2-Sep-2009] $)
$}

${
  $d x y f g $.
  $( Define the function which takes a topology and returns the path
  homotopy relation on that topology. $)
  df-phtpc $a |- =~ph = { <. x , y >. | ( x e. Top /\ y = { <. f , g >. |
        ( ( ( f e. ( II Cn x ) /\ g e. ( II Cn x ) ) /\ ( ( f ` 0 ) = ( g ` 0 )
      /\ ( f ` 1 ) = ( g ` 1 ) ) ) /\ ( f ( PHtpy ` x ) g ) =/= (/) ) } ) } $.
$}

${
  $d J x y f g $.
  $( The value of the path homotopy relation. $)
  phtpcval $p |- ( J e. Top -> ( =~ph ` J ) = { <. f , g >. |
        ( ( ( f e. ( II Cn J ) /\ g e. ( II Cn J ) ) /\ ( ( f ` 0 ) = ( g ` 0 )
      /\ ( f ` 1 ) = ( g ` 1 ) ) ) /\ ( f ( PHtpy ` J ) g ) =/= (/) ) } ) $=
    ( vx vy cv cii ccn co wcel wa cc0 cfv wceq c1 cphtpy c0 wne copab ctop
    cphtpc opreq2 eleq2d anbi12d anbi1d fveq2 opreqd neeq1d opabbidv df-phtpc
    cvv anass opabbii cxp oprex xpex opabssxp ssexi eqeltri fvopab4 ) DECAFZGDF
    ZHIZJZBFZVCJZKZLVAMLVEMNOVAMOVEMNKZKZVAVEVBPMZIZQRZKZABSVAGCHIZJZVEVNJZKZVH
    KZVAVECPMZIZQRZKZABSZTUAVBCNZVMWBABWDVIVRVLWAWDVGVQVHWDVDVOVFVPWDVCVNVAVBCG
    HUBZUCWDVCVNVEWEUCUDUEWDVKVTQWDVJVSVAVEVBCPUFUGUHUDUIDEABUJWCVQVHWAKZKZABSZ
    UKWBWGABVQVHWAULUMWHVNVNUNVNVNGCHUOZWIUPWFABVNVNUQURUSUT $.
    $( [2-Sep-2009] $)
$}

${
  $d J f g $. $d F f g $. $d G f g $. $d A f g $. $d B f g $.
  $( The relation "is path homotopic to". $)
  isphtpc $p |- ( ( J e. Top /\ F e. A /\ G e. B ) -> ( F ( =~ph ` J ) G <->
        ( ( ( F e. ( II Cn J ) /\ G e. ( II Cn J ) ) /\ ( ( F ` 0 ) = ( G ` 0 )
        /\ ( F ` 1 ) = ( G ` 1 ) ) ) /\ ( F ( PHtpy ` J ) G ) =/= (/) ) ) ) $=
    ( vf vg ctop wcel w3a cphtpc cfv wbr cv cii ccn co wa cc0 wceq c1 cphtpy
    c0 wne copab wb phtpcval breq syl 3ad2ant1 eleq1 anbi1d fveq1 eqeq1d
    anbi12d opreq1 neeq1d anbi2d eqeq2d opreq2 eqid brabg 3adant1 bitrd ) EHIZC
    AIZDBIZJCDEKLZMZCDFNZOEPQZIZGNZVKIZRZSVJLZSVMLZTZUAVJLZUAVMLZTZRZRZVJVMEUBL
    ZQZUCUDZRZFGUEZMZCVKIZDVKIZRZSCLZSDLZTZUACLZUADLZTZRZRZCDWDQZUCUDZRZVEVFVIW
    IUFZVGVEVHWHTXDFGEUGCDVHWHUHUIUJVFVGWIXCUFVEWGWJVNRZWMVQTZWPVTTZRZRZCVMWDQZ
    UCUDZRXCFGCDABWHVJCTZWCXIWFXKXLVOXEWBXHXLVLWJVNVJCVKUKULXLVRXFWAXGXLVPWMVQS
    VJCUMUNXLVSWPVTUAVJCUMUNUOUOXLWEXJUCVJCVMWDUPUQUOVMDTZXIWTXKXBXMXEWLXHWSXMV
    NWKWJVMDVKUKURXMXFWOXGWRXMVQWNWMSVMDUMUSXMVTWQWPUAVMDUMUSUOUOXMXJXAUCVMDCWD
    UTUQUOWHVAVBVCVD $.
    $( [2-Sep-2009] $)
$}

${
  $d J f g $.
  $( The relation "is path homotopic to". $)
  isphtpc2 $p |- ( ( J e. Top /\ G e. A ) -> ( F ( =~ph ` J ) G <->
        ( ( ( F e. ( II Cn J ) /\ G e. ( II Cn J ) ) /\ ( ( F ` 0 ) = ( G ` 0 )
        /\ ( F ` 1 ) = ( G ` 1 ) ) ) /\ ( F ( PHtpy ` J ) G ) =/= (/) ) ) ) $=
    ( vf vg ctop wcel wa cphtpc cfv wbr cii ccn co cc0 wceq c1 cphtpy c0 wne
    cvv wrel wi cv copab relopab wb phtpcval adantr releq syl mpbiri brrelex
    ex w3a isphtpc biimpd 3expa an1rs com23 mpdd simplll biimprd mpdi impbid )
    DGHZCAHZIZBCDJKZLZBMDNOZHZCVLHZIPBKPCKQRBKRCKQIZIBCDSKZOTUAZIZVIVKBUBHZVRVI
    VJUCZVKVSUDVIVTEUEZVLHFUEZVLHIPWAKPWBKQRWAKRWBKQIIWAWBVPOTUAIZEFUFZUCZWCEFU
    GVIVJWDQZVTWEUHVGWFVHEFDUIUJVJWDUKULUMVTVKVSBCVJUNUOULVIVSVKVRVIVSVKVRUDZVG
    VSVHWGVGVSVHWGVGVSVHUPVKVRUBABCDUQURUSUTUOVAVBVIVRVMVKVMVNVOVQVCVIVMVRVKVIV
    MVRVKUDZVGVMVHWHVGVMVHWHVGVMVHUPVKVRVLABCDUQVDUSUTUOVAVEVF $.
    $( [2-Sep-2009] $)
$}

${
  $d J r s t x y $.
  $( The domain of the path homotopy relation. $)
  phtpcdm $p |- ( J e. Top -> dom ( =~ph ` J ) = ( II Cn J ) ) $=
    ( vx vy vr vs vt ctop wcel cphtpc cfv cdm cii ccn co cv wbr wex wa cc0
    wceq c1 cphtpy c0 wne cvv wb visset isphtpc mp3an23 simplll syl6bi
    19.23adv eldm syl5ib pm3.2 pm2.43i eqid pm3.2i jctir adantl cicc copab2
    phtpyid ne0i syl jca isphtpc2 mpbird ex breq2 cla4ev syl6 syl6ibr impbid
    eqrdv ) AGHZBAIJZKZLAMNZVPBOZVRHZVTVSHZVPVTCOZVQPZCQZWBWAVPWDWBCVPWDWBWCVSH
    ZRSVTJZSWCJTUAVTJZUAWCJTRZRVTWCAUBJZNUCUDZRZWBVPVTUEHWCUEHWDWLUFBUGZCUGUEUE
    VTWCAUHUIWBWFWIWKUJUKULCVTVQWMUMZUNVPWBWEWAVPWBVTVTVQPZWEVPWBWOVPWBRZWOWBWB
    RZWGWGTZWHWHTZRZRZVTVTWJNZUCUDZRWPXAXCWBXAVPWBWQWTWBWQWBWBUOUPWRWSWGUQWHUQU
    RUSUTWPDOZSUAVANZHEOXEHRFOXDVTJTRDEFVBZXBHXCDEFVTXFAXFUQVCXBXFVDVEVFVSVTVTA
    VGVHVIWDWOCVTWMWCVTVTVQVJVKVLWNVMVNVO $.
    $( [2-Sep-2009] $)
$}

${
  $d J r s t v w x y z $.
  $( Path homotopy is an equivalence relation. Proposition 1.2 of Hatcher. $)
  phtpcer $p |- ( J e. Top -> Er ( =~ph ` J ) ) $=
    ( vx vy vz vw vr vs vt vv ctop wcel cv cphtpc cfv wbr wi wa wal wer cii
    ccn co cc0 wceq c1 cphtpy c0 wne pm3.22 eqcom biimpi anim12i ad2antrl wex
    w3a cicc cmin copab2 eqid phtpycom ne0i syl6 ex 3expb impr 19.23adv n0
    syl5ib jca cvv wb visset isphtpc mp3an23 3imtr4d simpll simprr eqtr an4s
    ad2ant2r adantl c2 cdiv cle cmul cif phtpyco expcom com23 df-3an biimpri
    adantrl sylan exp3a imp32 impcom anbi12d 19.21aivv 19.21aiv dfer2 sylibr )
    AJKZBLZCLZAMNZOZXDXCXEOZPZXFXDDLZXEOZQZXCXIXEOZPZQZDRCRZBRXESXBXOBXBXNCDXBX
    HXMXBXCTAUAUBZKZXDXPKZQZUCXCNZUCXDNZUDZUEXCNZUEXDNZUDZQZQZXCXDAUFNZUBZUGUHZ
    QZXRXQQZYAXTUDZYDYCUDZQZQZXDXCYHUBZUGUHZQZXFXGXBYKYSXBYKQYPYRYGYPXBYJXSYLYF
    YOXQXRUIYBYMYEYNYBYMXTYAUJUKYEYNYCYDUJUKULULUMXBYGYJYRXBYGQZELZYIKZEUNYRYJY
    TUUBYREXBXSYFUUBYRPZXBXQXRYFUUCPXBXQXRUOZYFUUCUUDYFQUUBFLZUCUEUPUBZKGLZUUFK
    QZHLZUUEUEUUGUQUBUUAUBUDQFGHURZYQKYRFGHXCXDUUAAUUJUUJUSUTYQUUJVAVBVCVDVEVFE
    YIVGVHVEVIVCXBXCVJKZXDVJKZXFYKVKBVLZCVLZVJVJXCXDAVMVNZXBUULUUKXGYSVKUUNUUMV
    JVJXDXCAVMVNVOXBYKXRXIXPKZQZYAUCXINZUDZYDUEXINZUDZQZQZXDXIYHUBZUGUHZQZQZXQU
    UPQZXTUURUDZYCUUTUDZQZQZXCXIYHUBZUGUHZQZXKXLXBUVGUVOXBUVGQUVLUVNUVGUVLXBYGU
    VCUVLYJUVEXSUUQYFUVBUVLXSUUQQZUVHYFUVBQZUVKUVPXQUUPXQXRUUQVPXSXRUUPVQVIYBUU
    SYEUVAUVKYBUUSQUVIYEUVAQUVJXTYAUURVRYCYDUUTVRULVSULVSVTWAUVGXBUVNYGUVCYJUVE
    XBUVNPZYGUVCQZYJUVEUVRUVSILZYIKZIUNUVEUVRPZYJUVSUWAUWBIUVSUVEUWAUVRUVSUUAUV
    DKZEUNUWAUVRPZUVEUVSUWCUWDEUVSUWAUWCUVRUVSUWAUWCUVRXSUUQYFUVBUWAUWCQZUVRPZX
    QXRUUPUOZUVQUWFUVPUWGUVQQZXBUWEUVNXBUWHUWEUVNPXBUWHQUWEUUHUUIUUGUEWBWCUBWDO
    UUEWBUUGWEUBZUVTUBUUEUWIUEUQUBUUAUBWFUDQFGHURZUVMKZUVNXBUWGUVQUWEUWKPFGHXCX
    DXIAUVTUUAUWJUWJUSWGVDUVMUWJVAVBWHWIXSUUPUWGXRUWGXSUUPQXQXRUUPWJWKWLWMVSWNW
    IVFEUVDVGVHWIVFIYIVGVHWOVSWPVIVCXBXFYKXJUVFUUOXBUULXIVJKZXJUVFVKUUNDVLZVJVJ
    XDXIAVMVNWQXBUUKUWLXLUVOVKUUMUWMVJVJXCXIAVMVNVOVIWRWSBCDXEWTXA $.
    $( [2-Sep-2009] $)
$}

$( (End of Jeff Madsen's mathbox.) $)


