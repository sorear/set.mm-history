$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intervals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Split a closed interval into the union of two closed intervals.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    iccsplit $p |- ( ( A e. RR /\ B e. RR /\ C e. ( A [,] B ) ) ->
                        ( A [,] B ) = ( ( A [,] C ) u. ( C [,] B ) ) ) $=
      ( cr wcel cicc co w3a cle wbr wa 3impia simp1 a1i simp2 3ad2ant3 3ad2ant2
      wi elicc2 wb vx cun cv wo clt simplr1 simplr2 simpr1 iccssre sseld adantr
      ltle syl11anc imp 3jca orcd simpr simplr3 olcd pm2.61ltlei ex simp3 letrd
      simp1r 3exp sylbid 3jcad simp1l jaod impbid 3adant3 3impa adantlr adantll
      imdistani ancoms orbi12d syl 3bitr4d elun syl6bbr eqrdv ) ADEZBDEZCABFGZE
      ZHZUAWEACFGZCBFGZUBZWGUAUCZWEEZWKWHEZWKWIEZUDZWKWJEWGWKDEZAWKIJZWKBIJZHZW
      PWQWKCIJZHZWPCWKIJZWRHZUDZWLWOWGWSXDWGWSXDWGWSKZXDWKCXEWKCUEJZKZXAXCXGWPW
      QWTWPWQWRWGXFUFWPWQWRWGXFUGXEXFWTXEWPCDEZXFWTRWGWPWQWRUHZWGXHWSWCWDWFXHWC
      WDKZWEDCABUIUJZLUKZWKCULUMUNUOUPXEXBKZXCXAXMWPXBWRWPWQWRWGXBUFXEXBUQWPWQW
      RWGXBURUOUSXIXLUTVAWGXAWSXCWGXAWPWQWRXAWPRWGWPWQWTMZNXAWQRWGWPWQWTONWCWDW
      FXAWRRZXJWFXHACIJZCBIJZHZXOABCSZXJXRXAWRXJXRXAHWKCBXAXJWPXRXNPXRXJXHXAXHX
      PXQMZQWCWDXRXAVDXAXJWTXRWPWQWTVBPXRXJXQXAXHXPXQVBQVCVEVFLVGWGXCWPWQWRXCWP
      RWGWPXBWRMZNWCWDWFXCWQRZXJWFXRYBXSXJXRXCWQXJXRXCHACWKWCWDXRXCVHXRXJXHXCXT
      QXCXJWPXRYAPXRXJXPXCXHXPXQOQXCXJXBXRWPXBWROPVCVEVFLXCWRRWGWPXBWRVBNVGVIVJ
      WCWDWLWSTWFABWKSVKWGXJXHKZWOXDTWCWDWFYCXJWFXHXKVOVLYCWMXAWNXCWCXHWMXATWDA
      CWKSVMWDXHWNXCTZWCXHWDYDCBWKSVPVNVQVRVSWKWHWIVTWAWB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $d D x $.
    $( Condition for a closed interval to be a subset of another closed
       interval.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    iccss $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) /\
                    ( A <_ C /\ D <_ B ) ) -> ( C [,] D ) C_ ( A [,] B ) ) $=
      ( vx cr wcel wa cle wbr w3a cicc co cv wi simp1 letrd ex wb elicc2 simpr1
      a1i simpl1l simpl2l simpl3l simpr2 simpl2r simpl1r simpl3r 3jcad 3ad2ant2
      simpr3 3ad2ant1 3imtr4d ssrdv ) AFGZBFGZHZCFGZDFGZHZACIJZDBIJZHZKZECDLMZA
      BLMZVEENZFGZCVHIJZVHDIJZKZVIAVHIJZVHBIJZKZVHVFGZVHVGGZVEVLVIVMVNVLVIOVEVI
      VJVKPUBVEVLVMVEVLHZACVHUPUQVAVDVLUCUSUTURVDVLUDVEVIVJVKUAZVBVCURVAVLUEVEV
      IVJVKUFQRVEVLVNVRVHDBVSUSUTURVDVLUGUPUQVAVDVLUHVEVIVJVKULVBVCURVAVLUIQRUJ
      VAURVPVLSVDCDVHTUKURVAVQVOSVDABVHTUMUNUO $.
      $( [2-Sep-2009] $)
  $}

  $( Condition for a closed interval to be a subset of another closed
     interval.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  iccss2 $p |- ( ( A e. RR /\ B e. RR ) -> ( ( C e. ( A [,] B ) /\
                    D e. ( A [,] B ) ) -> ( C [,] D ) C_ ( A [,] B ) ) ) $=
    ( cr wcel wa cicc co cle wbr w3a wss elicc2 anbi12d iccss 3expb expcom an4s
    wi 3adantr2 3adantl3 com12 sylbid ) AEFBEFGZCABHIZFZDUFFZGCEFZACJKZCBJKZLZD
    EFZADJKZDBJKZLZGZCDHIUFMZUEUGULUHUPABCNABDNOUQUEURUIUJUPUEURTZUKUIUJGUMUOUS
    UNUIUMUJUOUSUEUIUMGZUJUOGZGURUEUTVAURABCDPQRSUAUBUCUD $.
    $( [2-Sep-2009] $)

  ${
    iccshftr.1 $e |- ( A + R ) = C $.
    iccshftr.2 $e |- ( B + R ) = D $.
    $( Membership in a shifted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccshftr $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( X e. RR /\ R e. RR ) )
                  -> ( X e. ( A [,] B ) <-> ( X + R ) e. ( C [,] D ) ) ) $=
      ( cr wcel wa cle wbr w3a caddc co cicc wb leadd1 3expb simpl 2thd adantlr
      ax-addrcl adantl breq1i syl6bb an12s adantll breq2i elicc2 adantr readdcl
      3anbi123d syl5eqelr syl2an anandirs adantrl 3bitr4d ) AIJZBIJZKZFIJZEIJZK
      ZKZVCAFLMZFBLMZNZFEOPZIJZCVJLMZVJDLMZNZFABQPJZVJCDQPJZVFVCVKVGVLVHVMVEVCV
      KRVBVEVCVKVCVDUAFEUDUBUEVFVGAEOPZVJLMZVLUTVEVGVRRZVAUTVCVDVSAFESTUCVQCVJL
      GUFUGVFVHVJBEOPZLMZVMVAVEVHWARZUTVCVAVDWBVCVAVDWBFBESTUHUIVTDVJLHUJUGUNVB
      VOVIRVEABFUKULVBVDVPVNRZVCUTVAVDWCUTVDKZCIJDIJWCVAVDKZWDCVQIGAEUMUOWEDVTI
      HBEUMUOCDVJUKUPUQURUS $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccshftri.1 $e |- A e. RR $.
    iccshftri.2 $e |- B e. RR $.
    iccshftri.3 $e |- R e. RR $.
    iccshftri.4 $e |- ( A + R ) = C $.
    iccshftri.5 $e |- ( B + R ) = D $.
    $( Membership in a shifted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccshftri $p |- ( X e. ( A [,] B ) -> ( X + R ) e. ( C [,] D ) ) $=
      ( cr wcel cicc co caddc wss iccssre mp2an sseli wb iccshftr mpanl12 mpan2
      wa biimpd mpcom ) FLMZFABNOZMZFEPOCDNOMZUILFALMZBLMZUILQGHABRSTUHUJUKUHEL
      MZUJUKUAZIULUMUHUNUEUOGHABCDEFJKUBUCUDUFUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccshftl.1 $e |- ( A - R ) = C $.
    iccshftl.2 $e |- ( B - R ) = D $.
    $( Membership in a shifted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccshftl $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( X e. RR /\ R e. RR ) )
                  -> ( X e. ( A [,] B ) <-> ( X - R ) e. ( C [,] D ) ) ) $=
      ( cr wcel wa cle wbr w3a cmin co cicc wb resubcl lesub1 simpl 2thd adantl
      3expb adantlr breq1i syl6bb adantll breq2i 3anbi123d elicc2 adantr syl2an
      an12s syl5eqelr anandirs adantrl 3bitr4d ) AIJZBIJZKZFIJZEIJZKZKZVBAFLMZF
      BLMZNZFEOPZIJZCVILMZVIDLMZNZFABQPJZVICDQPJZVEVBVJVFVKVGVLVDVBVJRVAVDVBVJV
      BVCUAFESUBUCVEVFAEOPZVILMZVKUSVDVFVQRZUTUSVBVCVRAFETUDUEVPCVILGUFUGVEVGVI
      BEOPZLMZVLUTVDVGVTRZUSVBUTVCWAVBUTVCWAFBETUDUNUHVSDVILHUIUGUJVAVNVHRVDABF
      UKULVAVCVOVMRZVBUSUTVCWBUSVCKZCIJDIJWBUTVCKZWCCVPIGAESUOWDDVSIHBESUOCDVIU
      KUMUPUQUR $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccshftli.1 $e |- A e. RR $.
    iccshftli.2 $e |- B e. RR $.
    iccshftli.3 $e |- R e. RR $.
    iccshftli.4 $e |- ( A - R ) = C $.
    iccshftli.5 $e |- ( B - R ) = D $.
    $( Membership in a shifted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccshftli $p |- ( X e. ( A [,] B ) -> ( X - R ) e. ( C [,] D ) ) $=
      ( cr wcel cicc co cmin wss iccssre mp2an sseli wb iccshftl mpanl12 biimpd
      wa mpan2 mpcom ) FLMZFABNOZMZFEPOCDNOMZUILFALMZBLMZUILQGHABRSTUHUJUKUHELM
      ZUJUKUAZIULUMUHUNUEUOGHABCDEFJKUBUCUFUDUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccdil.1 $e |- ( A x. R ) = C $.
    iccdil.2 $e |- ( B x. R ) = D $.
    $( Membership in a dilated interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccdil $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( X e. RR /\ R e. RR+ ) )
                  -> ( X e. ( A [,] B ) <-> ( X x. R ) e. ( C [,] D ) ) ) $=
      ( cr wcel wa cle wbr w3a cmul co cicc wb sylan2 lemul1 crp rpre ax-mulrcl
      simpl 2thd adantl cc0 clt elrp syl3an3b 3expb adantlr breq1i syl6bb an12s
      adantll breq2i 3anbi123d elicc2 adantr remulcl syl5eqelr anandirs adantrl
      syl2an 3bitr4d ) AIJZBIJZKZFIJZEUAJZKZKZVJAFLMZFBLMZNZFEOPZIJZCVQLMZVQDLM
      ZNZFABQPJZVQCDQPJZVMVJVRVNVSVOVTVLVJVRRVIVLVJVRVJVKUDVKVJEIJZVREUBZFEUCSU
      EUFVMVNAEOPZVQLMZVSVGVLVNWGRZVHVGVJVKWHVKVGVJWDUGEUHMKZWHEUIZAFETUJUKULWF
      CVQLGUMUNVMVOVQBEOPZLMZVTVHVLVOWLRZVGVJVHVKWMVJVHVKWMVKVJVHWIWMWJFBETUJUK
      UOUPWKDVQLHUQUNURVIWBVPRVLABFUSUTVIVKWCWARZVJVKVIWDWNWEVGVHWDWNVGWDKZCIJD
      IJWNVHWDKZWOCWFIGAEVAVBWPDWKIHBEVAVBCDVQUSVEVCSVDVF $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccdili.1 $e |- A e. RR $.
    iccdili.2 $e |- B e. RR $.
    iccdili.3 $e |- R e. RR+ $.
    iccdili.4 $e |- ( A x. R ) = C $.
    iccdili.5 $e |- ( B x. R ) = D $.
    $( Membership in a dilated interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccdili $p |- ( X e. ( A [,] B ) -> ( X x. R ) e. ( C [,] D ) ) $=
      ( cr wcel cicc co cmul wss iccssre mp2an sseli wb wa iccdil mpanl12 mpan2
      crp biimpd mpcom ) FLMZFABNOZMZFEPOCDNOMZUJLFALMZBLMZUJLQGHABRSTUIUKULUIE
      UFMZUKULUAZIUMUNUIUOUBUPGHABCDEFJKUCUDUEUGUH $.
      $( [2-Sep-2009] $)
  $}

  ${
    icccntr.1 $e |- ( A / R ) = C $.
    icccntr.2 $e |- ( B / R ) = D $.
    $( Membership in a contracted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    icccntr $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( X e. RR /\ R e. RR+ ) )
                  -> ( X e. ( A [,] B ) <-> ( X / R ) e. ( C [,] D ) ) ) $=
      ( cr wcel wa cle wbr w3a cdiv co cicc wb rerpdivcl lediv1 crp 2thd adantl
      simpl cc0 clt elrp syl3an3b 3expb adantlr breq1i syl6bb adantll 3anbi123d
      an12s breq2i elicc2 adantr syl5eqelr syl2an anandirs adantrl 3bitr4d ) AI
      JZBIJZKZFIJZEUAJZKZKZVGAFLMZFBLMZNZFEOPZIJZCVNLMZVNDLMZNZFABQPJZVNCDQPJZV
      JVGVOVKVPVLVQVIVGVORVFVIVGVOVGVHUDFESUBUCVJVKAEOPZVNLMZVPVDVIVKWBRZVEVDVG
      VHWCVHVDVGEIJUEEUFMKZWCEUGZAFETUHUIUJWACVNLGUKULVJVLVNBEOPZLMZVQVEVIVLWGR
      ZVDVGVEVHWHVGVEVHWHVHVGVEWDWHWEFBETUHUIUOUMWFDVNLHUPULUNVFVSVMRVIABFUQURV
      FVHVTVRRZVGVDVEVHWIVDVHKZCIJDIJWIVEVHKZWJCWAIGAESUSWKDWFIHBESUSCDVNUQUTVA
      VBVC $.
      $( [2-Sep-2009] $)
  $}

  ${
    icccntri.1 $e |- A e. RR $.
    icccntri.2 $e |- B e. RR $.
    icccntri.3 $e |- R e. RR+ $.
    icccntri.4 $e |- ( A / R ) = C $.
    icccntri.5 $e |- ( B / R ) = D $.
    $( Membership in a contracted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    icccntri $p |- ( X e. ( A [,] B ) -> ( X / R ) e. ( C [,] D ) ) $=
      ( cr wcel cicc co cdiv wss iccssre mp2an sseli crp icccntr mpanl12 biimpd
      wb wa mpan2 mpcom ) FLMZFABNOZMZFEPOCDNOMZUJLFALMZBLMZUJLQGHABRSTUIUKULUI
      EUAMZUKULUEZIUMUNUIUOUFUPGHABCDEFJKUBUCUGUDUH $.
      $( [2-Sep-2009] $)
  $}

  $c II $.
  $( Extend class notation with the unit interval. $)
  cii $a class II $.

  $( Define the unit interval with the Euclidean topology. $)
  df-ii $a |- II = ( MetOpen ` ( ( abs o. - )
                          |` ( ( 0 [,] 1 ) X. ( 0 [,] 1 ) ) ) ) $.

  $( The unit interval is a topological space.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iitop $p |- II e. Top $=
    ( cabs cmin ccom cc0 c1 cicc co cxp cres cme wcel cii ctop eqid cnmet ax-mp
    metres df-ii opntop ) ABCZDEFGZUAHIZJKZLMKTJKUCTTNOTUAQPUBLRSP $.
    $( [2-Sep-2009] $)

  $( The base set of the unit interval.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iiuni $p |- ( 0 [,] 1 ) = U. II $=
    ( cii cuni cc0 c1 cicc co cabs cmin ccom cxp cres cme wcel ax-mp cc wss cdm
    wceq cr mp2an cnmet metres 0re 1re iccssre ax-resscn sstri cnmetba metssba2
    eqid df-ii uniopn2 eqcomi ) ABZCDEFZGHIZUOUOJKZLMZUNUORUPLMZURUPUPUJZUAZUPU
    OUBNUQAUOUSUOOPUOUQQQRVAUOSOCSMDSMUOSPUCUDCDUETUFUGUPOUOUPUTUHUITUKULNUM $.
    $( [2-Sep-2009] $)

  $( Alternate definition of the unit interval.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  dfii2 $p |- II = ( subSp ` <. ( 0 [,] 1 ) , ( topGen ` ran (,) ) >. ) $=
    ( cii cabs cmin ccom cc0 c1 cicc co cxp cres copn cfv cioo crn csubsp df-ii
    ctg cop cr wcel wss wceq 0re 1re iccssre mp2an stioo ax-mp eqtr4i ) ABCDEFG
    HZUJIJKLZUJMNQLROLZPUJSUAZULUKUBESTFSTUMUCUDEFUEUFUJUGUHUI $.
    $( [2-Sep-2009] $)

  $( Alternate definition of the unit interval.  (Contributed by Jeff Madsen,
     11-Jun-2010.) $)
  dfii3 $p |- II = ( subSp ` <. ( 0 [,] 1 ) ,
       ( MetOpen ` ( abs o. - ) ) >. ) $=
    ( cc0 c1 cicc cabs cmin ccom copn cfv cop csubsp cii cme wcel wss wceq eqid
    co cc cr mp2an cnmet 0re 1re iccssre ax-resscn sstri cxp cres cnmetba df-ii
    subtopmet eqcomi ) ABCQZDEFZGHZIJHZKUNLMUMRNUPKOUNUNPZUAUMSRASMBSMUMSNUBUCA
    BUDTUEUFUNUNUMUMUGUHZUOKRUMURPUNUQUIUOPUJUKTUL $.
    $( [11-Jun-2010] $)

  $( Reverse the unit interval.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  iirev $p |- ( X e. ( 0 [,] 1 ) -> ( 1 - X ) e. ( 0 [,] 1 ) ) $=
    ( cr wcel cc0 cle wbr c1 w3a cmin co cicc 1re resubcl mpan 3ad2ant1 sylancr
    wb 0re elicc2 mp2an simp3 simp1 subge0 mpbird simp2 subge02 mpbid 3imtr4i
    3jca ) ABCZDAEFZAGEFZHZGAIJZBCZDUNEFZUNGEFZHZADGKJZCZUNUSCZUMUOUPUQUJUKUOUL
    GBCZUJUOLGAMNOUMUPULUJUKULUAUMVBUJUPULQLUJUKULUBZGAUCPUDUMUKUQUJUKULUEUMVBU
    JUKUQQLVCGAUFPUGUIDBCZVBUTUMQRLDGASTVDVBVAURQRLDGUNSTUH $.
    $( [2-Sep-2009] $)

  $( Map the first half of ` II ` into ` II ` .  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iihalf1 $p |- ( X e. ( 0 [,] ( 1 / 2 ) ) -> ( 2 x. X ) e. ( 0 [,] 1 ) ) $=
    ( cr wcel cc0 cle wbr c1 c2 cdiv co w3a cicc 2re wa 0re wb 1re elicc2 mp2an
    2pos cmul remulcl mpan 3ad2ant1 ltleii mulge0 mpanl12 3adant3 clt lemuldiv2
    pm3.2i mp3an23 biimpar 3adant2 3jca 2ne0 rereccli 3imtr4i ) ABCZDAEFZAGHIJZ
    EFZKZHAUAJZBCZDVDEFZVDGEFZKZADVALJCZVDDGLJCZVCVEVFVGUSUTVEVBHBCZUSVEMHAUBUC
    UDUSUTVFVBVKDHEFUSUTNVFMDHOMTUEHAUFUGUHUSVBVGUTUSVGVBUSGBCZVKDHUIFZNVGVBPQV
    KVMMTUKAGHUJULUMUNUODBCZVABCVIVCPOHMUPUQDVAARSVNVLVJVHPOQDGVDRSUR $.
    $( [2-Sep-2009] $)

  $( Map the second half of ` II ` into ` II ` .  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iihalf2 $p |- ( X e. ( ( 1 / 2 ) [,] 1 ) ->
                                      ( ( 2 x. X ) - 1 ) e. ( 0 [,] 1 ) ) $=
    ( cr wcel c1 c2 cdiv co cle wbr w3a cmul cc0 cicc 2re 1re sylancl wb elicc2
    mp3an23 mp2an cmin remulcl mpan resubcl 3ad2ant1 subge0 clt pm3.2i ledivmul
    wa 2pos mp3an13 bitr4d biimpar 3adant3 caddc wceq ax-1cn 2timesi a1i breq2d
    lemul2 lesubadd syl 3bitr4d biimpa 3adant2 3jca 2ne0 rereccli 0re 3imtr4i )
    ABCZDEFGZAHIZADHIZJZEAKGZDUAGZBCZLVSHIZVSDHIZJZAVNDMGCZVSLDMGCZVQVTWAWBVMVO
    VTVPVMVRBCZDBCZVTEBCZVMWFNEAUBUCZOVRDUDPUEVMVOWAVPVMWAVOVMWADVRHIZVOVMWFWGW
    AWJQWIOVRDUFPWGVMWHLEUGIZUJZVOWJQOWHWKNUKUHZDAEUIULUMUNUOVMVPWBVOVMVPWBVMVR
    EDKGZHIZVRDDUPGZHIZVPWBVMWNWPVRHWNWPUQVMDURUSUTVAVMWGWLVPWOQOWMADEVBSVMWFWB
    WQQZWIWFWGWGWROOVRDDVCSVDVEVFVGVHVNBCWGWDVQQENVIVJOVNDARTLBCWGWEWCQVKOLDVSR
    TVL $.
    $( [2-Sep-2009] $)

  $( The unit interval is closed under multiplication.  (Contributed by Jeff
     Madsen, 2-Sep-2009.) $)
  iimulcl $p |- ( ( A e. ( 0 [,] 1 ) /\ B e. ( 0 [,] 1 ) )
                                  -> ( A x. B ) e. ( 0 [,] 1 ) ) $=
    ( cr wcel cc0 cle wbr c1 w3a wa cmul co cicc ax-mulrcl 3ad2antr1 1re wb 0re
    elicc2 mp2an 3ad2antl1 mulge0 3adantr3 3adantl3 an6 wi lemul12a mpanr2 an4s
    mpanl2 3impia sylbi ax-1cn mulid1i syl6breq 3jca anbi12i 3imtr4i ) ACDZEAFG
    ZAHFGZIZBCDZEBFGZBHFGZIZJZABKLZCDZEVHFGZVHHFGZIZAEHMLZDZBVMDZJVHVMDZVGVIVJV
    KUSUTVFVIVAUSVDVCVIVEABNOUAUSUTVFVJVAUSUTJZVCVDVJVEABUBUCUDVGVHHHKLZHFVGUSV
    CJZUTVDJZVAVEJZIVHVRFGZUSUTVAVCVDVEUEVSVTWAWBUSUTVCVDWAWBUFZVQHCDZVCVDJZWCP
    VQWDJWEWDWCPAHBHUGUHUJUIUKULHUMUNUOUPVNVBVOVFECDZWDVNVBQRPEHASTWFWDVOVFQRPE
    HBSTUQWFWDVPVLQRPEHVHSTUR $.
    $( [2-Sep-2009] $)

  ${
    iccst.1 $e |- R = ( topGen ` ran (,) ) $.
    iccst.2 $e |- J = ( MetOpen ` ( ( abs o. - ) |`
                                    ( ( A [,] B ) X. ( A [,] B ) ) ) ) $.
    $( The subspace topology induced by a closed interval.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    iccst $p |- ( ( A e. RR /\ B e. RR ) ->
                                J = ( subSp ` <. ( A [,] B ) , R >. ) ) $=
      ( cr wcel wa cabs cmin ccom cxp cres copn cfv wceq wss eqcomd eqid co cop
      cicc csubsp a1i iccssre xpss12 anidms resabs1 3syl cme remet remetba cioo
      fveq2d crn ctg tgioo eqtri subtopmet sylancr 3eqtrd ) AGHBGHIZDJKLZABUCUA
      ZVEMZNZOPZVDGGMZNZVFNZOPZVECUBUDPZDVHQVCFUEVCVGVKOVCVKVGVCVEGRZVFVIRZVKVG
      QABUFZVNVOVEGVEGUGUHVDVFVIUIUJSUOVCVJUKHZVNVLVMQVJVJTZULVPVQVNIVMVLVJVKCV
      LGVEVKTVJVRUMCUNUPUQPVJOPZEVJVSVRVSTURUSVLTUTSVAVB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d A v $.  $d B v $.  $d C v $.
    icoopnst.1 $e |- J = ( MetOpen ` ( ( abs o. - ) |`
                                    ( ( A [,] B ) X. ( A [,] B ) ) ) ) $.
    $( A half-open interval starting at ` A ` is open in the closed interval
       from ` A ` to ` B ` .  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    icoopnst $p |- ( ( A e. RR /\ B e. RR ) ->
                                ( C e. ( A (,] B ) -> ( A [,) C ) e. J ) ) $=
      ( vv cr wcel wa co wceq cle wbr clt w3a wi a1i adantr ex wb cioc cico crn
      cicc cioo ctg cfv cop csubsp cv cin wrex c1 cmin iooretop simp1 peano2rem
      ltm1 ltletr 3expb mpancom mpand impr 3adantr3 ad2antrr simp3 3jcad elioc2
      simp2 biimpa ltle 3adant2 3expa an31s imp ancom2s 3adantr2 anasss adantll
      syld an4s syldan jcad simpl1 simpr2 simpl3 impbid1 simpll simp1d syl11anc
      3jca elico2 elin cxr syl elioo2 elicc2 anbi12d syl5bb 3bitr4d eqrdv ineq1
      rexr eqeq2d rcla4ev sylancr ctop cvv retop oprex elsubsp mp2an eqid iccst
      sylibr eleqtrrd ) AGHZBGHZIZCABUAJHZACUBJZDHXSXTIZYAABUDJZUEUCUFUGZUHUIUG
      ZDYBYAFUJZYCUKZKZFYDULZYAYEHZYBAUMUNJZCUEJZYDHYAYLYCUKZKZYIYKCUOYBFYAYMYB
      YFGHZAYFLMZYFCNMZOZYOYKYFNMZYQOZYOYPYFBLMZOZIZYFYAHZYFYMHZYBYRUUCYBYRYTUU
      BYBYRYOYSYQYRYOPYBYOYPYQUPQZXQYRYSPXRXTXQYRYSXQYOYPYSYQXQYOYPYSXQYOIZYKAN
      MZYPYSXQUUHYOAURRYKGHZUUGUUHYPIYSPZXQUUIYOAUQZRUUIXQYOUUJYKAYFUSUTVAVBVCV
      DSVEYRYQPYBYOYPYQVFQVGYBYRYOYPUUAUUFYRYPPYBYOYPYQVIQXSXTCGHZACNMZCBLMZOZY
      RUUAPZXSXTUUOABCVHVJZXRUUOUUPXQXRUULUUNUUPUUMXRUULUUNUUPXRUULIZUUNIZYRUUA
      UUSYOYQUUAYPUURYOUUNYQUUAUURYOIZYQUUNUUAUUTYQUUNIZUUAYOUULXRUVAUUAPZYOUUL
      XRUVBYOUULXROUVAYFBNMZUUAYFCBUSYOXRUVCUUAPUULYFBVKVLVTVMVNVOVPWAVQSVRVQVS
      WBVGWCUUCYOYPYQYOYSYQUUBWDYTYOYPUUAWEYOYSYQUUBWFWKWGYBXQUULUUDYRTXQXRXTWH
      YBUULUUMUUNUUQWIZACYFWLWJUUEYFYLHZYFYCHZIYBUUCYFYLYCWMYBUVEYTUVFUUBYBYKWN
      HZCWNHZUVEYTTXQUVGXRXTXQUUIUVGUUKYKXCWOVEYBUULUVHUVDCXCWOYKCYFWPWJXSUVFUU
      BTXTABYFWQRWRWSWTXAYHYNFYLYDYFYLKYGYMYAYFYLYCXBXDXEXFYDXGHYCXHHYJYITXIABU
      DXJFYAYCYDXHXKXLXOXSDYEKXTABYDDYDXMEXNRXPS $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d A v $.  $d B v $.  $d C v $.
    iocopnst.1 $e |- J = ( MetOpen ` ( ( abs o. - ) |`
                                    ( ( A [,] B ) X. ( A [,] B ) ) ) ) $.
    $( A half-open interval ending at ` B ` is open in the closed interval from
       ` A ` to ` B ` .  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    iocopnst $p |- ( ( A e. RR /\ B e. RR ) ->
                                ( C e. ( A [,) B ) -> ( C (,] B ) e. J ) ) $=
      ( vv cr wcel wa co wceq clt wbr cle w3a wi a1i adantr ex wb cico cioc crn
      cicc cioo ctg cfv cop csubsp cv cin wrex c1 caddc iooretop simp1 peano2re
      simp2 ltp1 3expa ancom1s ancomsd mpdan mpand impr 3adantr2 ad2antlr 3jcad
      lelttr elico2 biimpa ltle 3adant2 syld imp 3adantr3 anasss adantlr syldan
      an4s jcad simpl1 simpl2 simpr3 3jca impbid1 simp1d simplr elioc2 syl11anc
      simp3 elin cxr rexr syl elioo2 elicc2 anbi12d syl5bb 3bitr4d eqrdv eqeq2d
      ineq1 rcla4ev sylancr ctop retop oprex elsubsp mp2an sylibr eqid eleqtrrd
      cvv iccst ) AGHZBGHZIZCABUAJHZCBUBJZDHXRXSIZXTABUDJZUEUCUFUGZUHUIUGZDYAXT
      FUJZYBUKZKZFYCULZXTYDHZYACBUMUNJZUEJZYCHXTYKYBUKZKZYHCYJUOYAFXTYLYAYEGHZC
      YELMZYEBNMZOZYNYOYEYJLMZOZYNAYENMZYPOZIZYEXTHZYEYLHZYAYQUUBYAYQYSUUAYAYQY
      NYOYRYQYNPYAYNYOYPUPQZYQYOPYAYNYOYPURQXQYQYRPXPXSXQYQYRXQYNYPYRYOXQYNYPYR
      XQYNIZBYJLMZYPYRXQUUGYNBUSRUUFYJGHZUUGYPIYRPXQUUHYNBUQZRUUFUUHIYPUUGYRYNX
      QUUHYPUUGIYRPZYNXQUUHUUJYEBYJVIUTVAVBVCVDVEVFSVGVHYAYQYNYTYPUUEXRXSCGHZAC
      NMZCBLMZOZYQYTPZXRXSUUNABCVJVKZXPUUNUUOXQXPUUKUULUUOUUMXPUUKUULUUOXPUUKIZ
      UULIZYQYTUURYNYOYTYPUUQYNUULYOYTUUQYNIUULYOIZYTXPUUKYNUUSYTPXPUUKYNOUUSAY
      ELMZYTACYEVIXPYNUUTYTPUUKAYEVLVMVNUTVOVTVPSVQVPVRVSYQYPPYAYNYOYPWKQVHWAUU
      BYNYOYPYNYOYRUUAWBYNYOYRUUAWCYSYNYTYPWDWEWFYAUUKXQUUCYQTYAUUKUULUUMUUPWGZ
      XPXQXSWHCBYEWIWJUUDYEYKHZYEYBHZIYAUUBYEYKYBWLYAUVBYSUVCUUAYACWMHZYJWMHZUV
      BYSTYAUUKUVDUVACWNWOXQUVEXPXSXQUUHUVEUUIYJWNWOVGCYJYEWPWJXRUVCUUATXSABYEW
      QRWRWSWTXAYGYMFYKYCYEYKKYFYLXTYEYKYBXCXBXDXEYCXFHYBXNHYIYHTXGABUDXHFXTYBY
      CXNXIXJXKXRDYDKXSABYCDYCXLEXORXMS $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $( A linear combination of two reals which lies in the interval between
       them.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    lincmb01cmp $p |- ( ( ( A e. RR /\ B e. RR /\ A < B ) /\ T e. ( 0 [,] 1 ) )
                    -> ( ( ( 1 - T ) x. A ) + ( T x. B ) ) e. ( A [,] B ) ) $=
      ( cr wcel cc0 c1 cicc co wa cmin cmul caddc wceq 0re ancoms adantr syl2an
      cc eqid clt wbr w3a recn anim12i wss 1re iccssre mp2an sseli recnd ax-1cn
      subdir mp3an1 mulid2 opreq1d eqtrd adantlr simpll ax-mulcl subadd23 subdi
      adantll syl111anc 3expb ancom2s eqcomd subcl mulcl sylan2 addcom syl11anc
      opreq2d 3eqtrd 3adantl3 adantl wb crp resubcl 3adant3 posdif biimp3a elrp
      sylanbrc iccdil mpanl12 biimpa an32s mpdan mul02 opreq12d syl eleqtrd a1i
      remulcl iccshftr syl22anc mpbid addid2 npcan eqeltrd ) ADEZBDEZABUAUBZUCZ
      CFGHIZEZJZGCKIALIZCBLIZMIZCBAKIZLIZAMIZABHIZXBXCXGXKXNNZXDXBXCJZASEZBSEZJ
      ZCSEZXPXGXBXRXCXSAUDZBUDZUEXGCXFDCFDEZGDEZXFDUFOUGFGUHUIUJZUKXTYAJZXKACAL
      IZKIZXJMIZAXJYHKIZMIZXNXRYAXKYJNXSXRYAJZXIYIXJMYMXIGALIZYHKIZYIYAXRXIYONZ
      GSEYAXRYPULGCAUMUNPYMYNAYHKXRYNANYAAUOQUPUQUPURYGXRYHSEZXJSEZYJYLNXRXSYAU
      SZXRYAYQXSYAXRYQCAUTPURXSYAYRXRYAXSYRCBUTPVCAYHXJVAVDYGYLAXMMIZXNYGYKXMAM
      YGXMYKYAXTXMYKNZYAXSXRUUAYAXSXRUUACBAVBVEVFPVGVMYGXRXMSEZYTXNNYSYAXTUUBXT
      YAXLSEZUUBXSXRUUCBAVHPCXLVIVJPAXMVKVLUQVNRVOXHXNFAMIZXLAMIZHIZXOXHXMFXLHI
      ZEZXNUUFEZXHXMFXLLIZGXLLIZHIZUUGXHCDEZXMUULEZXGUUMXEYFVPXEUUMXGUUNXEUUMJX
      GUUNUUMXEXGUUNVQZXEUUMXLVREZUUOXEXLDEZFXLUAUBZUUPXBXCUUQXDXCXBUUQBAVSPZVT
      XBXCXDUURABWAWBXLWCWDYDYEUUMUUPJUUOOUGFGUUJUUKXLCUUJTUUKTWEWFVJPWGWHWIXEU
      ULUUGNZXGXBXCUUTXDXQUUCUUTXQXLUUSUKUUCUUJFUUKXLHXLWJXLUOWKWLVTQWMXBXCXGUU
      HUUIVQZXDXQXGJZYDUUQXMDEZXBUVAYDUVBOWNXQUUQXGUUSQXGXQUVCXGUUMUUQUVCXQYFUU
      SCXLWORPXBXCXGUSFXLUUDUUEAXMUUDTUUETWPWQVOWRXEUUFXONZXGXBXCUVDXDXQUUDAUUE
      BHXBUUDANZXCXBXRUVEYBAWSWLQXCXBUUEBNZXCXSXRUVFXBYCYBBAWTRPWKVTQWMXA $.
      $( [2-Sep-2009] $)
  $}

  ${
    $( A linear combination of two reals which lies in the interval between
       them.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    lincmb01icc $p |- ( ( A e. RR /\ B e. RR ) ->
              ( ( C e. ( A [,] B ) /\ D e. ( A [,] B ) /\ T e. ( 0 [,] 1 ) ) ->
                      ( ( ( 1 - T ) x. C ) + ( T x. D ) ) e. ( A [,] B ) ) ) $=
      ( cr wcel wa cicc co cc0 c1 cmul caddc wceq imp 3adantr3 jca opreq1d cc
      w3a cmin clt wbr w3o iccssre sseld adantrr adantrl lttri4 syl11anc iccss2
      wss adantr simpr3 wi lincmb01cmp ex 3expa an32s sylan sseldd opreq2 mp2an
      0re 1re sseli recnd ad2antll ax-1cn npcan mpan subcl adddir mulid2 adantl
      ancri 3eqtr3d 3adantr1 sylan9eqr simplr2 eqeltrd ancom2s iirev recn mulcl
      syl2an ancoms adantll adantlr addcom 3adantl3 nncan eqcomd sylan2 3jaodan
      syl eqtrd mpdan ) AFGBFGHZCABIJZGZDXAGZEKLIJZGZUAZLEUBJZCMJZEDMJZNJZXAGZW
      TXFHZCDUCUDZCDOZDCUCUDZUEZXKWTXBXCXPXEWTXBXCHZHZCFGZDFGZXPWTXBXSXCWTXBXSW
      TXAFCABUFZUGPUHZWTXCXTXBWTXCXTWTXAFDYAUGPZUIZCDUJUKQXLXMXKXNXOXLXMHCDIJZX
      AXJXLYEXAUMZXMWTXBXCYFXEWTXQYFABCDULPQUNXLXSXTHZXEHXMXJYEGZXLYGXEWTXBXCYG
      XEXRXSXTYBYDRQWTXBXCXEUOZRYGXMXEYHYGXMHXEYHXSXTXMXEYHUPXSXTXMUAXEYHCDEUQU
      RUSPUTVAVBXLXNHXJDXAXNXLXJXGDMJZXINJZDXNXHYJXINCDXGMVCSWTXCXEYKDOZXBWTXCX
      EHHETGZDTGZYLXEYMWTXCXEEXDFEKFGLFGXDFUMVEVFKLUFVDZVGVHZVIWTXCYNXEWTXCHDYC
      VHUHYMYNHZXGENJZDMJZLDMJZYKDYQYRLDMYMYRLOZYNLTGZYMUUAVJLEVKVLUNSYMXGTGZYM
      HYNYSYKOZYMUUCUUBYMUUCVJLEVMVLVQUUCYMYNUUDXGEDVNUSVAYNYTDOYMDVOVPVRUKVSVT
      XBXCXEWTXNWAWBXLXOHDCIJZXAXJXLUUEXAUMZXOWTXBXCUUFXEWTXCXBUUFWTXCXBHUUFABD
      CULPWCQUNXLXTXSHZXEHZXOXJUUEGZXLUUGXEWTXBXCUUGXEXRXTXSYDYBRQYIRUUGXOXEUUI
      UUGXOHXEUUIXTXSXOXEUUIUPXTXSXOUAZXEUUIUUJXEHZXJLXGUBJZDMJZXHNJZUUEUUKXJXI
      XHNJZUUNXTXSXEXJUUOOZXOUUHXHTGZXITGZUUPXSXEUUQXTXEXSUUQXEUUCCTGUUQXSXEXGX
      EXGXDGZXGFGEWDZXDFXGYOVGWQVHCWEXGCWFWGWHWIXTXEUURXSXEXTUURXEYMYNUURXTYPDW
      EEDWFWGWHWJXHXIWKUKWLXEUUOUUNOZUUJXEYMUVAYPYMXIUUMXHNYMEUULDMYMUULEUUBYMU
      ULEOVJLEWMVLWNSSWQVPWRXEUUJUUSUUNUUEGUUTDCXGUQWOWBURUSPUTVAVBWPWSUR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v x y z $.  $d B u v x y z $.  $d C u v x y z $.
    $d K u v w x y z $.  $d R w z $.  $d S w z $.  $d F u v $.  $d G u v $.
    $d u v w x y z $.
    oprpiece1.1 $e |- A e. RR $.
    oprpiece1.2 $e |- B e. RR $.
    oprpiece1.3 $e |- A <_ B $.
    oprpiece1.4 $e |- R e. _V $.
    oprpiece1.5 $e |- S e. _V $.
    oprpiece1.6 $e |- K e. ( A [,] B ) $.
    oprpiece1.7 $e |- F = { <. <. x , y >. , z >. | ( ( x e. ( A [,] B )
                                /\ y e. C ) /\ z = if ( x <_ K , R , S ) ) } $.

    ${
      oprpiece1.8 $e |- G = { <. <. x , y >. , z >. |
                              ( ( x e. ( A [,] K ) /\ y e. C ) /\ z = R ) } $.
      $( Restriction to the first part of a piecewise defined function.
         (Contributed by Jeff Madsen, 11-Jun-2010.) $)
      oprpiece1res1 $p |- ( F |` ( ( A [,] K ) X. C ) ) = G $=
        ( wcel vu vv vw cicc co cxp cres wceq cv wa oprvres cle wbr csb cif cxr
        wral cr ax-mp w3a wb elicc1 mp2an simp1bi iccleub mp3an12 iftrue adantr
        rexr syl wss lbicc2 mp3an wi iccss2 sseli cvv visset csbex ax-17 hbcsb1
        ifex hbif breq1 csbeq1a ifbieq12d biidd oprabval2gf sylan 3eqtr4d eqtrd
        mp3an3 rgen2 wfn fnoprab2 ssid xpss12 fnssres eqfnoprv2 mpbir ) IDKUDUE
        ZFUFZUGZJUHZUAUIZUBUIZXCUEZXEXFJUEZUHZUBFUQUAXAUQZXIUAUBXAFXEXATZXFFTZU
        JZXGXEXFIUEZXHXEXFXAFIUKXMXEKULUMZBXFAXEGUNZUNZBXFAXEHUNZUNZUOZXQXNXHXK
        XTXQUHZXLXKXOYADUPTZKUPTZXKXODURTZYBLDVIUSZKDEUDUEZTZYCQYGYCDKULUMZKEUL
        UMZYBEUPTZYGYCYHYIUTVAYEEURTZYJMEVIUSDEKVBVCVDUSDKXEVEVFXOXQXSVGVJVHXKX
        EYFTZXLXNXTUHZXAYFXEDYFTZYGXAYFVKZYDYKDEULUMYNLMNDEVLVMQYDYKYNYGUJYOVNL
        MDEDKVOVCVCZVPYLXLXTVQTYMXOXQXSBXFXPUBVRZAXEGUAVRZOVSVSZBXFXRYQAXEHYRPV
        SVSWBABCUCXEXFYFFAUIZKULUMZGHUOZXTIXOXPXRUOVQUCUIZXETZAVTZUUDBVTZUUDCVT
        ZUUCXFTZBVTZUUHCVTZXOAUCXPXRXOAVTAUCXEGYRUUEWAZAUCXEHYRUUEWAWCXOBUCXQXS
        XOBVTBUCXFXPYQUUIWAZBUCXFXRYQUUIWAWCYTXEUHUUAXOGHXPXRYTXEKULWDAXEGWEZAX
        EHWEWFBUIXFUHZXOXOXPXRXQXSUUNXOWGBXFXPWEZBXFXRWEWFRWHWLWIXKXLXQVQTXHXQU
        HYSABCUCXEXFXAFGXQJXPVQUUEUUFUUGUUIUUJUUKUULUUMUUOSWHWLWJWKWMXCXBWNZJXB
        WNXDXJVAIYFFUFZWNXBUUQVKZUUPABCYFFUUBIUUAGHOPWBRWOYOFFVKUURYPFWPXAYFFFW
        QVCUUQXBIWRVCABCXAFGJOSWOUAUBXAFXCJWSVCWT $.
        $( [19-Dec-2013] $) $( [11-Jun-2010] $)
    $}

    ${
      $d P w x $.  $d Q w x $.
      oprpiece1.9 $e |- ( x = K -> R = P ) $.
      oprpiece1.10 $e |- ( x = K -> S = Q ) $.
      oprpiece1.11 $e |- ( y e. C -> P = Q ) $.
      oprpiece1.12 $e |- G = { <. <. x , y >. , z >. |
                              ( ( x e. ( K [,] B ) /\ y e. C ) /\ z = S ) } $.
      $( Restriction to the second part of a piecewise defined function.
         (Contributed by Jeff Madsen, 11-Jun-2010.) $)
      oprpiece1res2 $p |- ( F |` ( ( K [,] B ) X. C ) ) = G $=
        ( vu vv vw cicc co cxp cres wceq cv wral wcel wa oprvres cle wbr csb cr
        cif wn wo w3a wb elicc2 mp2an simp1bi ax-mp clt leloe ltnle orbi1d mpan
        bitrd biimpd imdistani 3adant3 sylbi iffalse adantl leid syl5ibrcom imp
        breq2 iftrue syl adantll wi ax-17 visset hbcsb1 hbeq hbim eleq1 csbeq1a
        eqeq12d imbi12d chvar adantr cvv csbeq1 elisseti csbief syl6eq csbeq2dv
        mpan2 eqcoms 3eqtr4d adantlr eqtrd jaodan anasss ancoms sylan wss mp3an
        ubicc2 iccss2 sseli csbex ifex breq1 ifbieq12d biidd oprabval2gf mp3an3
        hbif rgen2 wfn fnoprab2 ssid xpss12 fnssres eqfnoprv2 mpbir ) KMEUHUIZF
        UJZUKZLULZUEUMZUFUMZYTUIZUUBUUCLUIZULZUFFUNUEYRUNZUUFUEUFYRFUUBYRUOZUUC
        FUOZUPZUUDUUBUUCKUIZUUEUUBUUCYRFKUQUUJUUBMURUSZBUUCAUUBIUTZUTZBUUCAUUBJ
        UTZUTZVBZUUPUUKUUEUUHUUBVAUOZUULVCZMUUBULZVDZUPZUUIUUQUUPULZUUHUURMUUBU
        RUSZUUBEURUSZVEZUVBMVAUOZEVAUOZUUHUVFVFMDEUHUIZUOZUVGSUVJUVGDMURUSZMEUR
        USZDVAUOZUVHUVJUVGUVKUVLVEVFNODEMVGVHVIVJZOMEUUBVGVHUURUVDUVBUVEUURUVDU
        VAUURUVDUVAUVGUURUVDUVAVFUVNUVGUURUPZUVDMUUBVKUSZUUTVDUVAMUUBVLUVOUVPUU
        SUUTMUUBVMVNVPVOVQVRVSVTUUIUVBUVCUUIUURUVAUVCUUIUURUPZUUSUVCUUTUUSUVCUV
        QUULUUNUUPWAWBUVQUUTUPUUQUUNUUPUURUUTUUQUUNULZUUIUURUUTUPUULUVRUURUUTUU
        LUURUULUUTUUBUUBURUSUUBWCMUUBUUBURWFWDWEUULUUNUUPWGWHWIUUIUUTUUNUUPULUU
        RUUIUUTUPBUUCGUTZBUUCHUTZUUNUUPUUIUVSUVTULZUUTBUMZFUOZGHULZWJUUIUWAWJBU
        FUUIUWABUUIBWKBUGUGUVSUVTBUGUUCGUFWLZUGUMZUUCUOZBWKZWMBUGUUCHUWEUWHWMWN
        WOUWBUUCULZUWCUUIUWDUWAUWBUUCFWPUWIGUVSHUVTBUUCGWQBUUCHWQWRWSUCWTXAUUTU
        UNUVSULZUUIUWJUUBMUUBMULZUUCXBUOZUWJUWEUWKBUUCUUMGXBUWKUUMAMIUTGAUUBMIX
        CAUGMIGMUVISXDZUWFGUOAWKUAXEXFXGXHXIWBUUTUUPUVTULZUUIUWNUUBMUWKUWLUWNUW
        EUWKBUUCUUOHXBUWKUUOAMJUTHAUUBMJXCAUGMJHUWMUWFHUOAWKUBXEXFXGXHXIWBXJXKX
        LXMXNXOXPUUHUUBUVIUOZUUIUUKUUQULZYRUVIUUBUVJEUVIUOZYRUVIXQZSUVMUVHDEURU
        SUWQNOPDEXSXRUVMUVHUVJUWQUPUWRWJNODEMEXTVHVHZYAUWOUUIUUQXBUOUWPUULUUNUU
        PBUUCUUMUWEAUUBIUEWLZQYBYBBUUCUUOUWEAUUBJUWTRYBYBZYCABCUGUUBUUCUVIFAUMZ
        MURUSZIJVBZUUQKUULUUMUUOVBXBUWFUUBUOZAWKZUXEBWKZUXECWKZUWHUWGCWKZUULAUG
        UUMUUOUULAWKAUGUUBIUWTUXFWMAUGUUBJUWTUXFWMZYIUULBUGUUNUUPUULBWKBUGUUCUU
        MUWEUWHWMBUGUUCUUOUWEUWHWMZYIUXBUUBULUXCUULIJUUMUUOUXBUUBMURYDAUUBIWQAU
        UBJWQZYEUWIUULUULUUMUUOUUNUUPUWIUULYFBUUCUUMWQBUUCUUOWQZYETYGYHXPUUHUUI
        UUPXBUOUUEUUPULUXAABCUGUUBUUCYRFJUUPLUUOXBUXFUXGUXHUWHUXIUXJUXKUXLUXMUD
        YGYHXJXLYJYTYSYKZLYSYKUUAUUGVFKUVIFUJZYKYSUXOXQZUXNABCUVIFUXDKUXCIJQRYC
        TYLUWRFFXQUXPUWSFYMYRUVIFFYNVHUXOYSKYOVHABCYRFJLRUDYLUEUFYRFYTLYPVHYQ
        $.
        $( [19-Dec-2013] $) $( [11-Jun-2010] $)
    $}
  $}

