$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intervals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Split a closed interval into the union of two closed intervals.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    iccsplit $p |- ( ( A e. RR /\ B e. RR /\ C e. ( A [,] B ) ) ->
                        ( A [,] B ) = ( ( A [,] C ) u. ( C [,] B ) ) ) $=
      ( cr wcel cicc co w3a cle wbr wa 3impia simp1 a1i simp2 3ad2ant3 3ad2ant2
      wi elicc2 wb vx cun cv wo clt simplr1 simplr2 simpr1 iccssre sseld adantr
      ltle syl2anc imp 3jca orcd simpr simplr3 olcd pm2.61ltlei ex simp1r simp3
      letrd 3exp sylbid 3jcad simp1l jaod impbid 3adant3 imdistani 3impa ancoms
      adantlr adantll orbi12d syl 3bitr4d elun syl6bbr eqrdv ) ADEZBDEZCABFGZEZ
      HZUAWEACFGZCBFGZUBZWGUAUCZWEEZWKWHEZWKWIEZUDZWKWJEWGWKDEZAWKIJZWKBIJZHZWP
      WQWKCIJZHZWPCWKIJZWRHZUDZWLWOWGWSXDWGWSXDWGWSKZXDWKCXEWKCUEJZKZXAXCXGWPWQ
      WTWPWQWRWGXFUFWPWQWRWGXFUGXEXFWTXEWPCDEZXFWTRWGWPWQWRUHZWGXHWSWCWDWFXHWCW
      DKZWEDCABUIUJZLUKZWKCULUMUNUOUPXEXBKZXCXAXMWPXBWRWPWQWRWGXBUFXEXBUQWPWQWR
      WGXBURUOUSXIXLUTVAWGXAWSXCWGXAWPWQWRXAWPRWGWPWQWTMZNXAWQRWGWPWQWTONWCWDWF
      XAWRRZXJWFXHACIJZCBIJZHZXOABCSZXJXRXAWRXJXRXAHWKCBXAXJWPXRXNPXRXJXHXAXHXP
      XQMZQWCWDXRXAVBXAXJWTXRWPWQWTVCPXRXJXQXAXHXPXQVCQVDVEVFLVGWGXCWPWQWRXCWPR
      WGWPXBWRMZNWCWDWFXCWQRZXJWFXRYBXSXJXRXCWQXJXRXCHACWKWCWDXRXCVHXRXJXHXCXTQ
      XCXJWPXRYAPXRXJXPXCXHXPXQOQXCXJXBXRWPXBWROPVDVEVFLXCWRRWGWPXBWRVCNVGVIVJW
      CWDWLWSTWFABWKSVKWGXJXHKZWOXDTWCWDWFYCXJWFXHXKVLVMYCWMXAWNXCWCXHWMXATWDAC
      WKSVOWDXHWNXCTZWCXHWDYDCBWKSVNVPVQVRVSWKWHWIVTWAWB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $d D x $.
    $( Condition for a closed interval to be a subset of another closed
       interval.  (Moved into main set.mm as ~ iccss and may be deleted by
       mathbox owner, JM. --NM 29-May-2014.) $)
    iccssOLD $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) /\
                    ( A <_ C /\ D <_ B ) ) -> ( C [,] D ) C_ ( A [,] B ) ) $=
      ( iccss ) ABCDE $.
      $( [2-Sep-2009] $)
  $}

  $( Condition for a closed interval to be a subset of another closed
     interval.  (Moved into main set.mm as ~ iccss2 and may be deleted by
     mathbox owner, JM. --NM 29-May-2014.) $)
  iccss2OLD $p |- ( ( A e. RR /\ B e. RR ) -> ( ( C e. ( A [,] B ) /\
                    D e. ( A [,] B ) ) -> ( C [,] D ) C_ ( A [,] B ) ) ) $=
    ( iccss2 ) ABCDE $.
    $( [2-Sep-2009] $)

  ${
    iccshftr.1 $e |- ( A + R ) = C $.
    iccshftr.2 $e |- ( B + R ) = D $.
    $( Membership in a shifted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccshftr $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( X e. RR /\ R e. RR ) )
                  -> ( X e. ( A [,] B ) <-> ( X + R ) e. ( C [,] D ) ) ) $=
      ( cr wcel wa cle wbr w3a caddc co cicc wb leadd1 3expb simpl 2thd adantlr
      ax-addrcl adantl breq1i syl6bb an12s adantll breq2i elicc2 adantr readdcl
      3anbi123d syl5eqelr syl2an anandirs adantrl 3bitr4d ) AIJZBIJZKZFIJZEIJZK
      ZKZVCAFLMZFBLMZNZFEOPZIJZCVJLMZVJDLMZNZFABQPJZVJCDQPJZVFVCVKVGVLVHVMVEVCV
      KRVBVEVCVKVCVDUAFEUDUBUEVFVGAEOPZVJLMZVLUTVEVGVRRZVAUTVCVDVSAFESTUCVQCVJL
      GUFUGVFVHVJBEOPZLMZVMVAVEVHWARZUTVCVAVDWBVCVAVDWBFBESTUHUIVTDVJLHUJUGUNVB
      VOVIRVEABFUKULVBVDVPVNRZVCUTVAVDWCUTVDKZCIJDIJWCVAVDKZWDCVQIGAEUMUOWEDVTI
      HBEUMUOCDVJUKUPUQURUS $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccshftri.1 $e |- A e. RR $.
    iccshftri.2 $e |- B e. RR $.
    iccshftri.3 $e |- R e. RR $.
    iccshftri.4 $e |- ( A + R ) = C $.
    iccshftri.5 $e |- ( B + R ) = D $.
    $( Membership in a shifted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccshftri $p |- ( X e. ( A [,] B ) -> ( X + R ) e. ( C [,] D ) ) $=
      ( cr wcel cicc co caddc wss iccssre mp2an sseli wb iccshftr mpanl12 mpan2
      wa biimpd mpcom ) FLMZFABNOZMZFEPOCDNOMZUILFALMZBLMZUILQGHABRSTUHUJUKUHEL
      MZUJUKUAZIULUMUHUNUEUOGHABCDEFJKUBUCUDUFUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccshftl.1 $e |- ( A - R ) = C $.
    iccshftl.2 $e |- ( B - R ) = D $.
    $( Membership in a shifted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccshftl $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( X e. RR /\ R e. RR ) )
                  -> ( X e. ( A [,] B ) <-> ( X - R ) e. ( C [,] D ) ) ) $=
      ( cr wcel wa cle wbr w3a cmin co cicc wb resubcl lesub1 simpl 2thd adantl
      3expb adantlr breq1i syl6bb adantll breq2i 3anbi123d elicc2 adantr syl2an
      an12s syl5eqelr anandirs adantrl 3bitr4d ) AIJZBIJZKZFIJZEIJZKZKZVBAFLMZF
      BLMZNZFEOPZIJZCVILMZVIDLMZNZFABQPJZVICDQPJZVEVBVJVFVKVGVLVDVBVJRVAVDVBVJV
      BVCUAFESUBUCVEVFAEOPZVILMZVKUSVDVFVQRZUTUSVBVCVRAFETUDUEVPCVILGUFUGVEVGVI
      BEOPZLMZVLUTVDVGVTRZUSVBUTVCWAVBUTVCWAFBETUDUNUHVSDVILHUIUGUJVAVNVHRVDABF
      UKULVAVCVOVMRZVBUSUTVCWBUSVCKZCIJDIJWBUTVCKZWCCVPIGAESUOWDDVSIHBESUOCDVIU
      KUMUPUQUR $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccshftli.1 $e |- A e. RR $.
    iccshftli.2 $e |- B e. RR $.
    iccshftli.3 $e |- R e. RR $.
    iccshftli.4 $e |- ( A - R ) = C $.
    iccshftli.5 $e |- ( B - R ) = D $.
    $( Membership in a shifted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccshftli $p |- ( X e. ( A [,] B ) -> ( X - R ) e. ( C [,] D ) ) $=
      ( cr wcel cicc co cmin wss iccssre mp2an sseli wb iccshftl mpanl12 biimpd
      wa mpan2 mpcom ) FLMZFABNOZMZFEPOCDNOMZUILFALMZBLMZUILQGHABRSTUHUJUKUHELM
      ZUJUKUAZIULUMUHUNUEUOGHABCDEFJKUBUCUFUDUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccdil.1 $e |- ( A x. R ) = C $.
    iccdil.2 $e |- ( B x. R ) = D $.
    $( Membership in a dilated interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccdil $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( X e. RR /\ R e. RR+ ) )
                  -> ( X e. ( A [,] B ) <-> ( X x. R ) e. ( C [,] D ) ) ) $=
      ( cr wcel wa cle wbr w3a cmul co cicc wb sylan2 lemul1 crp rpre ax-mulrcl
      simpl 2thd adantl cc0 clt elrp syl3an3b 3expb adantlr breq1i syl6bb an12s
      adantll breq2i 3anbi123d elicc2 adantr remulcl syl5eqelr anandirs adantrl
      syl2an 3bitr4d ) AIJZBIJZKZFIJZEUAJZKZKZVJAFLMZFBLMZNZFEOPZIJZCVQLMZVQDLM
      ZNZFABQPJZVQCDQPJZVMVJVRVNVSVOVTVLVJVRRVIVLVJVRVJVKUDVKVJEIJZVREUBZFEUCSU
      EUFVMVNAEOPZVQLMZVSVGVLVNWGRZVHVGVJVKWHVKVGVJWDUGEUHMKZWHEUIZAFETUJUKULWF
      CVQLGUMUNVMVOVQBEOPZLMZVTVHVLVOWLRZVGVJVHVKWMVJVHVKWMVKVJVHWIWMWJFBETUJUK
      UOUPWKDVQLHUQUNURVIWBVPRVLABFUSUTVIVKWCWARZVJVKVIWDWNWEVGVHWDWNVGWDKZCIJD
      IJWNVHWDKZWOCWFIGAEVAVBWPDWKIHBEVAVBCDVQUSVEVCSVDVF $.
      $( [2-Sep-2009] $)
  $}

  ${
    iccdili.1 $e |- A e. RR $.
    iccdili.2 $e |- B e. RR $.
    iccdili.3 $e |- R e. RR+ $.
    iccdili.4 $e |- ( A x. R ) = C $.
    iccdili.5 $e |- ( B x. R ) = D $.
    $( Membership in a dilated interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccdili $p |- ( X e. ( A [,] B ) -> ( X x. R ) e. ( C [,] D ) ) $=
      ( cr wcel cicc co cmul wss iccssre mp2an sseli wb wa iccdil mpanl12 mpan2
      crp biimpd mpcom ) FLMZFABNOZMZFEPOCDNOMZUJLFALMZBLMZUJLQGHABRSTUIUKULUIE
      UFMZUKULUAZIUMUNUIUOUBUPGHABCDEFJKUCUDUEUGUH $.
      $( [2-Sep-2009] $)
  $}

  ${
    icccntr.1 $e |- ( A / R ) = C $.
    icccntr.2 $e |- ( B / R ) = D $.
    $( Membership in a contracted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    icccntr $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( X e. RR /\ R e. RR+ ) )
                  -> ( X e. ( A [,] B ) <-> ( X / R ) e. ( C [,] D ) ) ) $=
      ( cr wcel wa cle wbr w3a cdiv co cicc wb rerpdivcl lediv1 crp 2thd adantl
      simpl cc0 clt elrp syl3an3b 3expb adantlr breq1i syl6bb adantll 3anbi123d
      an12s breq2i elicc2 adantr syl5eqelr syl2an anandirs adantrl 3bitr4d ) AI
      JZBIJZKZFIJZEUAJZKZKZVGAFLMZFBLMZNZFEOPZIJZCVNLMZVNDLMZNZFABQPJZVNCDQPJZV
      JVGVOVKVPVLVQVIVGVORVFVIVGVOVGVHUDFESUBUCVJVKAEOPZVNLMZVPVDVIVKWBRZVEVDVG
      VHWCVHVDVGEIJUEEUFMKZWCEUGZAFETUHUIUJWACVNLGUKULVJVLVNBEOPZLMZVQVEVIVLWGR
      ZVDVGVEVHWHVGVEVHWHVHVGVEWDWHWEFBETUHUIUOUMWFDVNLHUPULUNVFVSVMRVIABFUQURV
      FVHVTVRRZVGVDVEVHWIVDVHKZCIJDIJWIVEVHKZWJCWAIGAESUSWKDWFIHBESUSCDVNUQUTVA
      VBVC $.
      $( [2-Sep-2009] $)
  $}

  ${
    icccntri.1 $e |- A e. RR $.
    icccntri.2 $e |- B e. RR $.
    icccntri.3 $e |- R e. RR+ $.
    icccntri.4 $e |- ( A / R ) = C $.
    icccntri.5 $e |- ( B / R ) = D $.
    $( Membership in a contracted interval.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    icccntri $p |- ( X e. ( A [,] B ) -> ( X / R ) e. ( C [,] D ) ) $=
      ( cr wcel cicc co cdiv wss iccssre mp2an sseli crp icccntr mpanl12 biimpd
      wb wa mpan2 mpcom ) FLMZFABNOZMZFEPOCDNOMZUJLFALMZBLMZUJLQGHABRSTUIUKULUI
      EUAMZUKULUEZIUMUNUIUOUFUPGHABCDEFJKUBUCUGUDUH $.
      $( [2-Sep-2009] $)
  $}

  $c II $.
  $( Extend class notation with the unit interval. $)
  cii $a class II $.

  $( Define the unit interval with the Euclidean topology. $)
  df-ii $a |- II = ( MetOpen ` ( ( abs o. - )
                          |` ( ( 0 [,] 1 ) X. ( 0 [,] 1 ) ) ) ) $.

  $( The unit interval is a topological space.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iitop $p |- II e. Top $=
    ( cabs cmin ccom cc0 c1 cicc co cxp cres cme wcel cii ctop eqid cnmet ax-mp
    metres df-ii opntop ) ABCZDEFGZUAHIZJKZLMKTJKUCTTNOTUAQPUBLRSP $.
    $( [2-Sep-2009] $)

  $( The base set of the unit interval.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iiuni $p |- ( 0 [,] 1 ) = U. II $=
    ( cabs cmin ccom cme wcel cc0 c1 cicc co cxp cres cii cuni wceq eqid cc wss
    cdm cr mp2an cnmet 0re 1re iccssre ax-resscn sstri cnmetba metssba2 uniopn2
    metres df-ii mp2b ) ABCZDEZUMFGHIZUOJKZDEUOLMNUMUMOZUAZUMUOUJUPLUOUNUOPQUOU
    PRRNURUOSPFSEGSEUOSQUBUCFGUDTUEUFUMPUOUMUQUGUHTUKUIUL $.
    $( [15-Jan-2014] $) $( [2-Sep-2009] $)

  $( Alternate definition of the unit interval.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  dfii2 $p |- II = ( subSp ` <. ( 0 [,] 1 ) , ( topGen ` ran (,) ) >. ) $=
    ( cii cabs cmin ccom cc0 c1 cicc co cxp cres copn cfv cioo crn csubsp df-ii
    ctg cop cr wcel wss wceq 0re 1re iccssre mp2an stioo ax-mp eqtr4i ) ABCDEFG
    HZUJIJKLZUJMNQLROLZPUJSUAZULUKUBESTFSTUMUCUDEFUEUFUJUGUHUI $.
    $( [2-Sep-2009] $)

  $( Alternate definition of the unit interval.  (Contributed by Jeff Madsen,
     11-Jun-2010.) $)
  dfii3 $p |- II = ( subSp ` <. ( 0 [,] 1 ) ,
       ( MetOpen ` ( abs o. - ) ) >. ) $=
    ( cc0 c1 cicc cabs cmin ccom copn cfv cop csubsp cii cme wcel wss wceq eqid
    co cc cr mp2an cnmet 0re 1re iccssre ax-resscn sstri cxp cres cnmetba df-ii
    subtopmet eqcomi ) ABCQZDEFZGHZIJHZKUNLMUMRNUPKOUNUNPZUAUMSRASMBSMUMSNUBUCA
    BUDTUEUFUNUNUMUMUGUHZUOKRUMURPUNUQUIUOPUJUKTUL $.
    $( [11-Jun-2010] $)
  ${
    $d x A $.  $d x B $.  $d x ph $.
    cnmptre.1 $e |- R = ( MetOpen ` ( abs o. - ) ) $.
    cnmptre.2 $e |- J = ( subSp ` <. A , ( topGen ` ran (,) ) >. ) $.
    cnmptre.3 $e |- K = ( subSp ` <. B , ( topGen ` ran (,) ) >. ) $.
    cnmptre.4 $e |- ( ph -> A C_ RR ) $.
    cnmptre.5 $e |- ( ph -> B C_ RR ) $.
    cnmptre.6 $e |- ( ( ph /\ x e. A ) -> F e. B ) $.
    cnmptre.7 $e |- ( ph -> ( x e. CC |-> F ) e. ( R Cn R ) ) $.
    $( Lemma for ~ iirevcn and related functions.  (Contributed by Mario
       Carneiro, 6-Jun-2014.) $)
    cnmptre $p |- ( ph -> ( x e. A |-> F ) e. ( J Cn K ) ) $=
      ( cfv ccn wcel cc eqid cmpt cop csubsp cabs cmin ccom cme cuni wceq cnmet
      cnmetba uniopn2 ax-mp ctop opntop a1i ax-resscn syl6ss cnmpt1res cioo crn
      co ctg cxp cres copn wss stioo syl subtopmet sylancr eqtr4d syl5eq oveq1d
      cr eleqtrrd wb cvv retop ssexg sylancl stoig3 syl5eqel wf cv ex fmptd frn
      reex cnsubsp2 syl22anc mpbid oveq2d ) ABCFUAZGDEUBUCPZQVBZGHQVBAWNGEQVBZR
      ZWNWPRZAWNCEUBUCPZEQVBWQABFEWTESCUDUEUFZUGRZSEUHUIXAXATZUJZXAESXAXCUKZIUL
      UMZWTTEUNRZAXBXGXDXAEIUOUMUPZXHACVOSLUQURZOUSAGWTEQAGCUTVAVCPZUBUCPZWTJAX
      KXACCVDVEZVFPZWTACVOVGZXKXMUILCVHVIAXBCSVGWTXMUIXDXIXAXLEXMSCXLTXEIXMTVJV
      KVLVMVNVPAGUNRXGWNVADVGZDSVGZWRWSVQAGXKUNJAXJUNRCVRRZXKUNRVSAXNVOVRRXQLWI
      CVOVRVTWACXJVRWBVKWCXHACDWNWDXOABCFDWNABWECRFDRNWFWNTWGCDWNWHVIADVOSMUQUR
      ZDWNGESXFWJWKWLAHWOGQAHDXJUBUCPZWOKAXSXADDVDVEZVFPZWOADVOVGXSYAUIMDVHVIAX
      BXPWOYAUIXDXRXAXTEYASDXTTXEIYATVJVKVLVMWMVP $.
      $( [6-Jun-2014] $)
  $}

  $( Reverse the unit interval.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  iirev $p |- ( X e. ( 0 [,] 1 ) -> ( 1 - X ) e. ( 0 [,] 1 ) ) $=
    ( cr wcel cc0 cle wbr c1 w3a cmin co cicc 1re resubcl mpan 3ad2ant1 sylancr
    wb 0re elicc2 mp2an simp3 simp1 subge0 mpbird simp2 subge02 mpbid 3imtr4i
    3jca ) ABCZDAEFZAGEFZHZGAIJZBCZDUNEFZUNGEFZHZADGKJZCZUNUSCZUMUOUPUQUJUKUOUL
    GBCZUJUOLGAMNOUMUPULUJUKULUAUMVBUJUPULQLUJUKULUBZGAUCPUDUMUKUQUJUKULUEUMVBU
    JUKUQQLVCGAUFPUGUIDBCZVBUTUMQRLDGASTVDVBVAURQRLDGUNSTUH $.
    $( [2-Sep-2009] $)
  ${
    $d x y $.
    $( The reversion function is a continuous map of the unit interval.
       (Contributed by Mario Carneiro, 6-Jun-2014.) $)
    iirevcn $p |- ( x e. ( 0 [,] 1 ) |-> ( 1 - x ) ) e. ( II Cn II ) $=
      ( cc0 c1 cicc co cv cmin cmpt cii ccn wcel wtru cabs ccom dfii2 cr a1i cc
      eqid ax-mp copn cfv wss 0re 1re iccssre mp2an iirev adantl cme cuni cnmet
      wceq cnmetba uniopn2 ctop opntop ax-1cn cnmptc cnmptid ctx subcn cnmpt12f
      cnmptre trud ) ABCDEZCAFZGEZHIIJEKLAVFVFMGNZUAUBZVHIIVJSZOOVFPUCZLBPKCPKV
      LUDUEBCUFUGQZVMVGVFKVHVFKLVGUHUILACVGGVJVJVJVJRRVIUJKZRVJUKUMVIVISZULZVIV
      JRVIVOUNVKUOTZVJUPKZLVNVRVPVIVJVKUQTQZVQVSVSLACVJVJRRVQVSVQVSCRKLURQUSLAV
      JRVQVSUTVSGVJVJVAEVJJEKLVIVJVOVKVBQVCVDVE $.
      $( [6-Jun-2014] $)
  $}

  $( Map the first half of ` II ` into ` II ` .  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iihalf1 $p |- ( X e. ( 0 [,] ( 1 / 2 ) ) -> ( 2 x. X ) e. ( 0 [,] 1 ) ) $=
    ( cr wcel cc0 cle wbr c1 c2 cdiv co w3a cicc 2re wa 0re wb 1re elicc2 mp2an
    2pos cmul remulcl mpan 3ad2ant1 ltleii mulge0 mpanl12 3adant3 clt lemuldiv2
    pm3.2i mp3an23 biimpar 3adant2 3jca 2ne0 rereccli 3imtr4i ) ABCZDAEFZAGHIJZ
    EFZKZHAUAJZBCZDVDEFZVDGEFZKZADVALJCZVDDGLJCZVCVEVFVGUSUTVEVBHBCZUSVEMHAUBUC
    UDUSUTVFVBVKDHEFUSUTNVFMDHOMTUEHAUFUGUHUSVBVGUTUSVGVBUSGBCZVKDHUIFZNVGVBPQV
    KVMMTUKAGHUJULUMUNUODBCZVABCVIVCPOHMUPUQDVAARSVNVLVJVHPOQDGVDRSUR $.
    $( [2-Sep-2009] $)
  ${
    $d x y $.
    iihalf1cn.1 $e |- J =
      ( subSp ` <. ( 0 [,] ( 1 / 2 ) ) , ( topGen ` ran (,) ) >. ) $.
    $( The first half function is a continuous map.  (Contributed by Mario
       Carneiro, 6-Jun-2014.) $)
    iihalf1cn $p |- ( x e. ( 0 [,] ( 1 / 2 ) ) |-> ( 2 x. x ) )
                      e. ( J Cn II ) $=
      ( cc0 c1 c2 co cicc cmul cii ccn wcel wtru eqid cr wss 0re 1re a1i cc cfv
      cdiv cmpt cabs cmin ccom copn dfii2 rehalfcl ax-mp iccssre iihalf1 adantl
      cv mp2an cme cuni wceq cnmetba uniopn2 ctop opntop syl 2cn cnmptc cnmptid
      cnmet ctx mulcn cnmpt12f cnmptre trud ) ADEFUBGZHGZFAUNZIGZUCBJKGLMAVNDEH
      GZUDUEUFZUGUAZVPBJVSNZCUHVNOPZMDOLZVMOLZWAQEOLZWCREUIUJDVMUKUOSVQOPZMWBWD
      WEQRDEUKUOSVOVNLVPVQLMVOULUMMAFVOIVSVSVSVSTTVRUPLZTVSUQURVRVRNZVGZVRVSTVR
      WGUSVTUTUJZMWFVSVALWFMWHSVRVSVTVBVCZWIWJWJMAFVSVSTTWIWJWIWJFTLMVDSVEMAVST
      WIWJVFWJIVSVSVHGVSKGLMVRVSWGVTVISVJVKVL $.
      $( [6-Jun-2014] $)
  $}

  $( Map the second half of ` II ` into ` II ` .  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  iihalf2 $p |- ( X e. ( ( 1 / 2 ) [,] 1 ) ->
                                      ( ( 2 x. X ) - 1 ) e. ( 0 [,] 1 ) ) $=
    ( cr wcel c1 c2 cdiv co cle wbr w3a cmul cc0 cicc 2re 1re sylancl wb elicc2
    mp3an23 mp2an cmin remulcl mpan resubcl 3ad2ant1 subge0 clt pm3.2i ledivmul
    wa 2pos mp3an13 bitr4d biimpar 3adant3 caddc wceq ax-1cn 2timesi a1i breq2d
    lemul2 lesubadd syl 3bitr4d biimpa 3adant2 3jca 2ne0 rereccli 0re 3imtr4i )
    ABCZDEFGZAHIZADHIZJZEAKGZDUAGZBCZLVSHIZVSDHIZJZAVNDMGCZVSLDMGCZVQVTWAWBVMVO
    VTVPVMVRBCZDBCZVTEBCZVMWFNEAUBUCZOVRDUDPUEVMVOWAVPVMWAVOVMWADVRHIZVOVMWFWGW
    AWJQWIOVRDUFPWGVMWHLEUGIZUJZVOWJQOWHWKNUKUHZDAEUIULUMUNUOVMVPWBVOVMVPWBVMVR
    EDKGZHIZVRDDUPGZHIZVPWBVMWNWPVRHWNWPUQVMDURUSUTVAVMWGWLVPWOQOWMADEVBSVMWFWB
    WQQZWIWFWGWGWROOVRDDVCSVDVEVFVGVHVNBCWGWDVQQENVIVJOVNDARTLBCWGWEWCQVKOLDVSR
    TVL $.
    $( [2-Sep-2009] $)
  ${
    $d x y $.
    iihalf2cn.1 $e |- J =
      ( subSp ` <. ( ( 1 / 2 ) [,] 1 ) , ( topGen ` ran (,) ) >. ) $.
    $( The second half function is a continuous map.  (Contributed by Mario
       Carneiro, 6-Jun-2014.) $)
    iihalf2cn $p |- ( x e. ( ( 1 / 2 ) [,] 1 ) |-> ( ( 2 x. x ) - 1 ) )
                      e. ( J Cn II ) $=
      ( c1 c2 co cicc cmul cmin cii ccn wcel wtru cc0 eqid cr wss 1re a1i cc cv
      cdiv cmpt cabs ccom cfv dfii2 rehalfcl ax-mp iccssre mp2an iihalf2 adantl
      copn 0re cme cuni wceq cnmetba uniopn2 ctop opntop syl 2cn cnmptc cnmptid
      cnmet ctx mulcn cnmpt12f recnd subcn cnmptre trud ) ADEUBFZDGFZEAUAZHFZDI
      FZUCBJKFLMAVPNDGFZUDIUEZUNUFZVSBJWBOZCUGVPPQZMVOPLZDPLZWDWFWERDUHUIRVODUJ
      UKSVTPQZMNPLWFWGUORNDUJUKSVQVPLVSVTLMVQULUMMAVRDIWBWBWBWBTTWAUPLZTWBUQURW
      AWAOZVGZWAWBTWAWIUSWCUTUIZMWHWBVALWHMWJSWAWBWCVBVCZWKWLWLMAEVQHWBWBWBWBTT
      WKWLWKWLWLMAEWBWBTTWKWLWKWLETLMVDSVEMAWBTWKWLVFWLHWBWBVHFWBKFZLMWAWBWIWCV
      ISVJMADWBWBTTWKWLWKWLMDWFMRSVKVEWLIWMLMWAWBWIWCVLSVJVMVN $.
      $( [6-Jun-2014] $)
  $}

  $( Divide the unit interval into two pieces.  (Contributed by Mario Carneiro,
     7-Jun-2014.) $)
  elii1 $p |- ( X e. ( 0 [,] ( 1 / 2 ) ) <->
                ( X e. ( 0 [,] 1 ) /\ X <_ ( 1 / 2 ) ) ) $=
    ( cc0 c1 co cicc cle wbr wa cr w3a wb 0re 1re elicc2 mp2an a1i df-3an bitri
    wcel anbi1i c2 rehalfcl ax-mp simp1bi simp3bi halflt1 ltleii letrd pm4.71ri
    cdiv ancom an32 3bitr4i 3bitri ) ABCUAUJDZEDSZACFGZUPHUPUQHZABCEDSZAUOFGZHZ
    UPUQUPAUOCUPAISZBAFGZUTBISZUOISZUPVBVCUTJZKLCISZVEMCUBUCZBUOANOZUDVEUPVHPVG
    UPMPUPVBVCUTVIUEUOCFGUPUOCVHMUFUGPUHUIUQUPUKVBVCHZUTHZUQHVJUQHZUTHURVAVJUTU
    QULUPVKUQUPVFVKVIVBVCUTQRTUSVLUTUSVBVCUQJZVLVDVGUSVMKLMBCANOVBVCUQQRTUMUN
    $.
    $( [7-Jun-2014] $)

  $( Divide the unit interval into two pieces.  (Contributed by Mario Carneiro,
     7-Jun-2014.) $)
  elii2 $p |- ( ( X e. ( 0 [,] 1 ) /\ -. X <_ ( 1 / 2 ) ) ->
                X e. ( ( 1 / 2 ) [,] 1 ) ) $=
    ( cc0 c1 cicc co wcel c2 cdiv cle wbr wn wa cr w3a wb 0re 1re elicc2 adantr
    mp2an simp1bi wo rehalfcl ax-mp letric sylancl ord imp simp3bi syl3anbrc )
    ABCDEFZACGHEZIJZKZLAMFZULAIJZACIJZAULCDEFZUKUOUNUKUOBAIJZUQBMFCMFZUKUOUSUQN
    OPQBCARTZUAZSUKUNUPUKUMUPUKUOULMFZUMUPUBVBUTVCQCUCUDZAULUEUFUGUHUKUQUNUKUOU
    SUQVAUISVCUTURUOUPUQNOVDQULCARTUJ $.
    $( [7-Jun-2014] $)

  $( The unit interval is closed under multiplication.  (Contributed by Jeff
     Madsen, 2-Sep-2009.) $)
  iimulcl $p |- ( ( A e. ( 0 [,] 1 ) /\ B e. ( 0 [,] 1 ) )
                                  -> ( A x. B ) e. ( 0 [,] 1 ) ) $=
    ( cr wcel cc0 cle wbr c1 w3a wa cmul co cicc ax-mulrcl 3ad2antr1 1re wb 0re
    elicc2 mp2an 3ad2antl1 mulge0 3adantr3 3adantl3 an6 wi lemul12a mpanr2 an4s
    mpanl2 3impia sylbi ax-1cn mulid1i syl6breq 3jca anbi12i 3imtr4i ) ACDZEAFG
    ZAHFGZIZBCDZEBFGZBHFGZIZJZABKLZCDZEVHFGZVHHFGZIZAEHMLZDZBVMDZJVHVMDZVGVIVJV
    KUSUTVFVIVAUSVDVCVIVEABNOUAUSUTVFVJVAUSUTJZVCVDVJVEABUBUCUDVGVHHHKLZHFVGUSV
    CJZUTVDJZVAVEJZIVHVRFGZUSUTVAVCVDVEUEVSVTWAWBUSUTVCVDWAWBUFZVQHCDZVCVDJZWCP
    VQWDJWEWDWCPAHBHUGUHUJUIUKULHUMUNUOUPVNVBVOVFECDZWDVNVBQRPEHASTWFWDVOVFQRPE
    HBSTUQWFWDVPVLQRPEHVHSTUR $.
    $( [2-Sep-2009] $)

  ${
    $d x y $.
    $( Multiplication is a continuous function on the unit interval.
       (Contributed by Mario Carneiro, 8-Jun-2014.) $)
    iimulcn $p |- ( x e. ( 0 [,] 1 ) , y e. ( 0 [,] 1 ) |-> ( x x. y ) )
                  e. ( ( II tX II ) Cn II ) $=
      ( cc0 c1 co cv cmul cmpt2 cii ccn wcel wtru eqid ax-mp dfii3 ctop a1i wss
      cc cr cicc ctx cabs cmin ccom copn cfv cop csubsp cuni wceq cnmet cnmetba
      cme uniopn2 opntop 0re 1re iccssre mp2an ax-resscn sstri cxp wf ax-mulopr
      wfn ffn fnov2 mpbi mulcn eqeltrri cnmpt2res trud iitop txtop wral iimulcl
      crn wb rgen2a fmpt2 frn sylbi cnsubsp2 mp4an oveq2i eleqtrri ) ABCDUAEZWH
      AFZBFZGEZHZIIUBEZWHUCUDUEZUFUGZUHUIUGZJEZWMIJEWLWMWOJEKZWLWQKZWRLABWKWOIW
      OWOIWHSWHSWNUNKZSWOUJUKWNWNMZULZWNWOSWNXAUMWOMZUONZOWOPKZLWTXEXBWNWOXCUPN
      ZQZXGWHSRZLWHTSCTKDTKWHTRUQURCDUSUTVAVBZQZXDOXGXJABSSWKHZWOWOUBEWOJEZKLGX
      KXLGSSVCZVFZGXKUKXMSGVDXNVEXMSGVGNABSSGVHVIWNWOXAXCVJVKQVLVMWMPKZXEWLVRWH
      RZXHWRWSVSIPKZXQXOVNVNIIVOUTXFWKWHKZBWHVPAWHVPZXPXRABWHWIWJVQVTXSWHWHVCZW
      HWLVDXPABWHWHWKWHWLWLMWAXTWHWLWBWCNXIWHWLWMWOSXDWDWEVIIWPWMJOWFWG $.
      $( [8-Jun-2014] $)
  $}

  ${
    iccst.1 $e |- R = ( topGen ` ran (,) ) $.
    iccst.2 $e |- J = ( MetOpen ` ( ( abs o. - ) |`
                                    ( ( A [,] B ) X. ( A [,] B ) ) ) ) $.
    $( The subspace topology induced by a closed interval.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    iccst $p |- ( ( A e. RR /\ B e. RR ) ->
                                J = ( subSp ` <. ( A [,] B ) , R >. ) ) $=
      ( cr wcel wa cabs cmin ccom cxp cres copn cfv wceq wss eqcomd eqid co cop
      cicc csubsp a1i iccssre xpss12 anidms resabs1 3syl cme remet remetba cioo
      fveq2d crn ctg tgioo eqtri subtopmet sylancr 3eqtrd ) AGHBGHIZDJKLZABUCUA
      ZVEMZNZOPZVDGGMZNZVFNZOPZVECUBUDPZDVHQVCFUEVCVGVKOVCVKVGVCVEGRZVFVIRZVKVG
      QABUFZVNVOVEGVEGUGUHVDVFVIUIUJSUOVCVJUKHZVNVLVMQVJVJTZULVPVQVNIVMVLVJVKCV
      LGVEVKTVJVRUMCUNUPUQPVJOPZEVJVSVRVSTURUSVLTUTSVAVB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d A v $.  $d B v $.  $d C v $.
    icoopnst.1 $e |- J = ( MetOpen ` ( ( abs o. - ) |`
                                    ( ( A [,] B ) X. ( A [,] B ) ) ) ) $.
    $( A half-open interval starting at ` A ` is open in the closed interval
       from ` A ` to ` B ` .  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    icoopnst $p |- ( ( A e. RR /\ B e. RR ) ->
                                ( C e. ( A (,] B ) -> ( A [,) C ) e. J ) ) $=
      ( vv cr wcel wa co wceq cle wbr clt w3a wi a1i adantr ex wb cioc cico crn
      cicc cioo ctg cfv cop csubsp cv cin wrex c1 cmin iooretop simp1 peano2rem
      ltm1 ltletr 3expb mpancom mpand impr 3adantr3 ad2antrr simp3 3jcad elioc2
      simp2 biimpa ltle 3adant2 3expa an31s imp ancom2s 3adantr2 anasss adantll
      syld an4s syldan jcad simpl1 simpr2 simpl3 3jca simpll simp1d elico2 elin
      impbid1 syl2anc cxr rexr elioo2 elicc2 anbi12d syl5bb 3bitr4d eqrdv ineq1
      syl eqeq2d rcla4ev sylancr ctop cvv retop ovex elsubsp mp2an sylibr iccst
      eqid eleqtrrd ) AGHZBGHZIZCABUAJHZACUBJZDHXSXTIZYAABUDJZUEUCUFUGZUHUIUGZD
      YBYAFUJZYCUKZKZFYDULZYAYEHZYBAUMUNJZCUEJZYDHYAYLYCUKZKZYIYKCUOYBFYAYMYBYF
      GHZAYFLMZYFCNMZOZYOYKYFNMZYQOZYOYPYFBLMZOZIZYFYAHZYFYMHZYBYRUUCYBYRYTUUBY
      BYRYOYSYQYRYOPYBYOYPYQUPQZXQYRYSPXRXTXQYRYSXQYOYPYSYQXQYOYPYSXQYOIZYKANMZ
      YPYSXQUUHYOAURRYKGHZUUGUUHYPIYSPZXQUUIYOAUQZRUUIXQYOUUJYKAYFUSUTVAVBVCVDS
      VEYRYQPYBYOYPYQVFQVGYBYRYOYPUUAUUFYRYPPYBYOYPYQVIQXSXTCGHZACNMZCBLMZOZYRU
      UAPZXSXTUUOABCVHVJZXRUUOUUPXQXRUULUUNUUPUUMXRUULUUNUUPXRUULIZUUNIZYRUUAUU
      SYOYQUUAYPUURYOUUNYQUUAUURYOIZYQUUNUUAUUTYQUUNIZUUAYOUULXRUVAUUAPZYOUULXR
      UVBYOUULXROUVAYFBNMZUUAYFCBUSYOXRUVCUUAPUULYFBVKVLVTVMVNVOVPWAVQSVRVQVSWB
      VGWCUUCYOYPYQYOYSYQUUBWDYTYOYPUUAWEYOYSYQUUBWFWGWLYBXQUULUUDYRTXQXRXTWHYB
      UULUUMUUNUUQWIZACYFWJWMUUEYFYLHZYFYCHZIYBUUCYFYLYCWKYBUVEYTUVFUUBYBYKWNHZ
      CWNHZUVEYTTXQUVGXRXTXQUUIUVGUUKYKWOXCVEYBUULUVHUVDCWOXCYKCYFWPWMXSUVFUUBT
      XTABYFWQRWRWSWTXAYHYNFYLYDYFYLKYGYMYAYFYLYCXBXDXEXFYDXGHYCXHHYJYITXIABUDX
      JFYAYCYDXHXKXLXMXSDYEKXTABYDDYDXOEXNRXPS $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d A v $.  $d B v $.  $d C v $.
    iocopnst.1 $e |- J = ( MetOpen ` ( ( abs o. - ) |`
                                    ( ( A [,] B ) X. ( A [,] B ) ) ) ) $.
    $( A half-open interval ending at ` B ` is open in the closed interval from
       ` A ` to ` B ` .  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    iocopnst $p |- ( ( A e. RR /\ B e. RR ) ->
                                ( C e. ( A [,) B ) -> ( C (,] B ) e. J ) ) $=
      ( vv cr wcel wa co wceq clt wbr cle w3a wi a1i adantr ex wb cico cioc crn
      cicc cioo ctg cfv cop csubsp cv cin wrex c1 caddc iooretop simp1 peano2re
      simp2 ltp1 3expa ancom1s ancomsd mpdan mpand impr 3adantr2 ad2antlr 3jcad
      lelttr elico2 biimpa ltle 3adant2 syld imp 3adantr3 anasss adantlr syldan
      an4s simp3 jcad simpl1 simpl2 simpr3 impbid1 simp1d simplr elioc2 syl2anc
      3jca elin cxr syl elioo2 elicc2 anbi12d syl5bb 3bitr4d eqrdv ineq1 eqeq2d
      rexr rcla4ev sylancr ctop cvv retop ovex mp2an sylibr eqid iccst eleqtrrd
      elsubsp ) AGHZBGHZIZCABUAJHZCBUBJZDHXRXSIZXTABUDJZUEUCUFUGZUHUIUGZDYAXTFU
      JZYBUKZKZFYCULZXTYDHZYACBUMUNJZUEJZYCHXTYKYBUKZKZYHCYJUOYAFXTYLYAYEGHZCYE
      LMZYEBNMZOZYNYOYEYJLMZOZYNAYENMZYPOZIZYEXTHZYEYLHZYAYQUUBYAYQYSUUAYAYQYNY
      OYRYQYNPYAYNYOYPUPQZYQYOPYAYNYOYPURQXQYQYRPXPXSXQYQYRXQYNYPYRYOXQYNYPYRXQ
      YNIZBYJLMZYPYRXQUUGYNBUSRUUFYJGHZUUGYPIYRPXQUUHYNBUQZRUUFUUHIYPUUGYRYNXQU
      UHYPUUGIYRPZYNXQUUHUUJYEBYJVIUTVAVBVCVDVEVFSVGVHYAYQYNYTYPUUEXRXSCGHZACNM
      ZCBLMZOZYQYTPZXRXSUUNABCVJVKZXPUUNUUOXQXPUUKUULUUOUUMXPUUKUULUUOXPUUKIZUU
      LIZYQYTUURYNYOYTYPUUQYNUULYOYTUUQYNIUULYOIZYTXPUUKYNUUSYTPXPUUKYNOUUSAYEL
      MZYTACYEVIXPYNUUTYTPUUKAYEVLVMVNUTVOVTVPSVQVPVRVSYQYPPYAYNYOYPWAQVHWBUUBY
      NYOYPYNYOYRUUAWCYNYOYRUUAWDYSYNYTYPWEWKWFYAUUKXQUUCYQTYAUUKUULUUMUUPWGZXP
      XQXSWHCBYEWIWJUUDYEYKHZYEYBHZIYAUUBYEYKYBWLYAUVBYSUVCUUAYACWMHZYJWMHZUVBY
      STYAUUKUVDUVACXCWNXQUVEXPXSXQUUHUVEUUIYJXCWNVGCYJYEWOWJXRUVCUUATXSABYEWPR
      WQWRWSWTYGYMFYKYCYEYKKYFYLXTYEYKYBXAXBXDXEYCXFHYBXGHYIYHTXHABUDXIFXTYBYCX
      GXOXJXKXRDYDKXSABYCDYCXLEXMRXNS $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $( A linear combination of two reals which lies in the interval between
       them.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    lincmb01cmp $p |- ( ( ( A e. RR /\ B e. RR /\ A < B ) /\ T e. ( 0 [,] 1 ) )
                    -> ( ( ( 1 - T ) x. A ) + ( T x. B ) ) e. ( A [,] B ) ) $=
      ( cr wcel cc0 c1 cicc co wa cmin cmul caddc wceq 0re ancoms adantr syl2an
      cc eqid clt wbr w3a recn anim12i wss 1re iccssre mp2an sseli recnd ax-1cn
      subdir mp3an1 mulid2 oveq1d eqtrd adantlr simpll ax-mulcl adantll syl3anc
      subadd23 subdi 3expb ancom2s eqcomd oveq2d sylan2 addcom syl2anc 3adantl3
      subcl mulcl 3eqtrd adantl wb resubcl 3adant3 posdif biimp3a elrp sylanbrc
      crp iccdil mpanl12 an32s mpdan mul02 oveq12d syl eleqtrd remulcl iccshftr
      biimpa a1i syl22anc mpbid addid2 npcan eqeltrd ) ADEZBDEZABUAUBZUCZCFGHIZ
      EZJZGCKIALIZCBLIZMIZCBAKIZLIZAMIZABHIZXBXCXGXKXNNZXDXBXCJZASEZBSEZJZCSEZX
      PXGXBXRXCXSAUDZBUDZUEXGCXFDCFDEZGDEZXFDUFOUGFGUHUIUJZUKXTYAJZXKACALIZKIZX
      JMIZAXJYHKIZMIZXNXRYAXKYJNXSXRYAJZXIYIXJMYMXIGALIZYHKIZYIYAXRXIYONZGSEYAX
      RYPULGCAUMUNPYMYNAYHKXRYNANYAAUOQUPUQUPURYGXRYHSEZXJSEZYJYLNXRXSYAUSZXRYA
      YQXSYAXRYQCAUTPURXSYAYRXRYAXSYRCBUTPVAAYHXJVCVBYGYLAXMMIZXNYGYKXMAMYGXMYK
      YAXTXMYKNZYAXSXRUUAYAXSXRUUACBAVDVEVFPVGVHYGXRXMSEZYTXNNYSYAXTUUBXTYAXLSE
      ZUUBXSXRUUCBAVMPCXLVNVIPAXMVJVKUQVORVLXHXNFAMIZXLAMIZHIZXOXHXMFXLHIZEZXNU
      UFEZXHXMFXLLIZGXLLIZHIZUUGXHCDEZXMUULEZXGUUMXEYFVPXEUUMXGUUNXEUUMJXGUUNUU
      MXEXGUUNVQZXEUUMXLWDEZUUOXEXLDEZFXLUAUBZUUPXBXCUUQXDXCXBUUQBAVRPZVSXBXCXD
      UURABVTWAXLWBWCYDYEUUMUUPJUUOOUGFGUUJUUKXLCUUJTUUKTWEWFVIPWOWGWHXEUULUUGN
      ZXGXBXCUUTXDXQUUCUUTXQXLUUSUKUUCUUJFUUKXLHXLWIXLUOWJWKVSQWLXBXCXGUUHUUIVQ
      ZXDXQXGJZYDUUQXMDEZXBUVAYDUVBOWPXQUUQXGUUSQXGXQUVCXGUUMUUQUVCXQYFUUSCXLWM
      RPXBXCXGUSFXLUUDUUEAXMUUDTUUETWNWQVLWRXEUUFXONZXGXBXCUVDXDXQUUDAUUEBHXBUU
      DANZXCXBXRUVEYBAWSWKQXCXBUUEBNZXCXSXRUVFXBYCYBBAWTRPWJVSQWLXA $.
      $( [2-Sep-2009] $)
  $}

  ${
    $( A linear combination of two reals which lies in the interval between
       them.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    lincmb01icc $p |- ( ( A e. RR /\ B e. RR ) ->
              ( ( C e. ( A [,] B ) /\ D e. ( A [,] B ) /\ T e. ( 0 [,] 1 ) ) ->
                      ( ( ( 1 - T ) x. C ) + ( T x. D ) ) e. ( A [,] B ) ) ) $=
      ( cr wcel wa cicc co cc0 c1 cmul caddc wceq imp 3adantr3 jca oveq1d cc wi
      w3a cmin clt wbr w3o iccssre adantrr adantrl lttri4 syl2anc iccss2 adantr
      sseld wss simpr3 lincmb01cmp 3expa an32s sylan sseldd oveq2 0re 1re mp2an
      sseli recnd ad2antll ax-1cn npcan mpan subcl adddir mulid2 adantl 3eqtr3d
      ex ancri 3adantr1 sylan9eqr simplr2 eqeltrd ancom2s iirev syl recn syl2an
      mulcl ancoms adantll adantlr addcom 3adantl3 nncan eqcomd sylan2 3jaodan
      eqtrd mpdan ) AFGBFGHZCABIJZGZDXAGZEKLIJZGZUBZLEUCJZCMJZEDMJZNJZXAGZWTXFH
      ZCDUDUEZCDOZDCUDUEZUFZXKWTXBXCXPXEWTXBXCHZHZCFGZDFGZXPWTXBXSXCWTXBXSWTXAF
      CABUGZUNPUHZWTXCXTXBWTXCXTWTXAFDYAUNPZUIZCDUJUKQXLXMXKXNXOXLXMHCDIJZXAXJX
      LYEXAUOZXMWTXBXCYFXEWTXQYFABCDULPQUMXLXSXTHZXEHXMXJYEGZXLYGXEWTXBXCYGXEXR
      XSXTYBYDRQWTXBXCXEUPZRYGXMXEYHYGXMHXEYHXSXTXMXEYHUAXSXTXMUBXEYHCDEUQVQURP
      USUTVAXLXNHXJDXAXNXLXJXGDMJZXINJZDXNXHYJXINCDXGMVBSWTXCXEYKDOZXBWTXCXEHHE
      TGZDTGZYLXEYMWTXCXEEXDFEKFGLFGXDFUOVCVDKLUGVEZVFVGZVHWTXCYNXEWTXCHDYCVGUH
      YMYNHZXGENJZDMJZLDMJZYKDYQYRLDMYMYRLOZYNLTGZYMUUAVILEVJVKUMSYMXGTGZYMHYNY
      SYKOZYMUUCUUBYMUUCVILEVLVKVRUUCYMYNUUDXGEDVMURUTYNYTDOYMDVNVOVPUKVSVTXBXC
      XEWTXNWAWBXLXOHDCIJZXAXJXLUUEXAUOZXOWTXBXCUUFXEWTXCXBUUFWTXCXBHUUFABDCULP
      WCQUMXLXTXSHZXEHZXOXJUUEGZXLUUGXEWTXBXCUUGXEXRXTXSYDYBRQYIRUUGXOXEUUIUUGX
      OHXEUUIXTXSXOXEUUIUAXTXSXOUBZXEUUIUUJXEHZXJLXGUCJZDMJZXHNJZUUEUUKXJXIXHNJ
      ZUUNXTXSXEXJUUOOZXOUUHXHTGZXITGZUUPXSXEUUQXTXEXSUUQXEUUCCTGUUQXSXEXGXEXGX
      DGZXGFGEWDZXDFXGYOVFWEVGCWFXGCWHWGWIWJXTXEUURXSXEXTUURXEYMYNUURXTYPDWFEDW
      HWGWIWKXHXIWLUKWMXEUUOUUNOZUUJXEYMUVAYPYMXIUUMXHNYMEUULDMYMUULEUUBYMUULEO
      VILEWNVKWOSSWEVOWRXEUUJUUSUUNUUEGUUTDCXGUQWPWBVQURPUSUTVAWQWSVQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v x y z $.  $d B u v x y z $.  $d C u v x y z $.
    $d K u v w x y z $.  $d R w z $.  $d S w z $.  $d F u v $.  $d G u v $.
    $d u v w x y z $.
    oprpiece1.1 $e |- A e. RR $.
    oprpiece1.2 $e |- B e. RR $.
    oprpiece1.3 $e |- A <_ B $.
    oprpiece1.4 $e |- R e. _V $.
    oprpiece1.5 $e |- S e. _V $.
    oprpiece1.6 $e |- K e. ( A [,] B ) $.
    oprpiece1.7 $e |- F = { <. <. x , y >. , z >. | ( ( x e. ( A [,] B )
                                /\ y e. C ) /\ z = if ( x <_ K , R , S ) ) } $.

    ${
      oprpiece1.8 $e |- G = { <. <. x , y >. , z >. |
                              ( ( x e. ( A [,] K ) /\ y e. C ) /\ z = R ) } $.
      $( Restriction to the first part of a piecewise defined function.
         (Contributed by Jeff Madsen, 11-Jun-2010.) $)
      oprpiece1res1 $p |- ( F |` ( ( A [,] K ) X. C ) ) = G $=
        ( wcel vu vv vw cicc co cxp cres wceq cv wral ovres cle wbr csb cif cxr
        wa cr rexr ax-mp w3a wb elicc1 mp2an simp1bi iccleub mp3an12 iftrue syl
        adantr wss lbicc2 mp3an wi iccss2 sseli cvv vex csbex ifex ax-17 hbcsb1
        hbif breq1 csbeq1a ifbieq12d biidd ov2gf mp3an3 sylan 3eqtr4d eqtrd wfn
        rgen2 fnoprab2 ssid xpss12 fnssres eqfnov2 mpbir ) IDKUDUEZFUFZUGZJUHZU
        AUIZUBUIZXCUEZXEXFJUEZUHZUBFUJUAXAUJZXIUAUBXAFXEXATZXFFTZUQZXGXEXFIUEZX
        HXEXFXAFIUKXMXEKULUMZBXFAXEGUNZUNZBXFAXEHUNZUNZUOZXQXNXHXKXTXQUHZXLXKXO
        YADUPTZKUPTZXKXODURTZYBLDUSUTZKDEUDUEZTZYCQYGYCDKULUMZKEULUMZYBEUPTZYGY
        CYHYIVAVBYEEURTZYJMEUSUTDEKVCVDVEUTDKXEVFVGXOXQXSVHVIVJXKXEYFTZXLXNXTUH
        ZXAYFXEDYFTZYGXAYFVKZYDYKDEULUMYNLMNDEVLVMQYDYKYNYGUQYOVNLMDEDKVOVDVDZV
        PYLXLXTVQTYMXOXQXSBXFXPUBVRZAXEGUAVRZOVSVSZBXFXRYQAXEHYRPVSVSVTABCUCXEX
        FYFFAUIZKULUMZGHUOZXTIXOXPXRUOVQUCUIZXETZAWAZUUDBWAZUUDCWAZUUCXFTZBWAZU
        UHCWAZXOAUCXPXRXOAWAAUCXEGYRUUEWBZAUCXEHYRUUEWBWCXOBUCXQXSXOBWABUCXFXPY
        QUUIWBZBUCXFXRYQUUIWBWCYTXEUHUUAXOGHXPXRYTXEKULWDAXEGWEZAXEHWEWFBUIXFUH
        ZXOXOXPXRXQXSUUNXOWGBXFXPWEZBXFXRWEWFRWHWIWJXKXLXQVQTXHXQUHYSABCUCXEXFX
        AFGXQJXPVQUUEUUFUUGUUIUUJUUKUULUUMUUOSWHWIWKWLWNXCXBWMZJXBWMXDXJVBIYFFU
        FZWMXBUUQVKZUUPABCYFFUUBIUUAGHOPVTRWOYOFFVKUURYPFWPXAYFFFWQVDUUQXBIWRVD
        ABCXAFGJOSWOUAUBXAFXCJWSVDWT $.
        $( [19-Dec-2013] $) $( [11-Jun-2010] $)
    $}

    ${
      $d P w x $.  $d Q w x $.
      oprpiece1.9 $e |- ( x = K -> R = P ) $.
      oprpiece1.10 $e |- ( x = K -> S = Q ) $.
      oprpiece1.11 $e |- ( y e. C -> P = Q ) $.
      oprpiece1.12 $e |- G = { <. <. x , y >. , z >. |
                              ( ( x e. ( K [,] B ) /\ y e. C ) /\ z = S ) } $.
      $( Restriction to the second part of a piecewise defined function.
         (Contributed by Jeff Madsen, 11-Jun-2010.) $)
      oprpiece1res2 $p |- ( F |` ( ( K [,] B ) X. C ) ) = G $=
        ( vu vv vw cicc co cxp cres wceq cv wral wa ovres cle wbr csb cif cr wn
        wcel wo w3a wb elicc2 mp2an simp1bi ax-mp leloe ltnle orbi1d bitrd mpan
        clt biimpd imdistani 3adant3 sylbi iffalse adantl leid breq2 syl5ibrcom
        imp iftrue syl adantll ax-17 vex hbcsb1 hbeq hbim eleq1 csbeq1a eqeq12d
        wi imbi12d chvar adantr cvv csbeq1 elexi csbief syl6eq csbeq2dv 3eqtr4d
        mpan2 eqcoms adantlr eqtrd jaodan anasss ancoms sylan wss ubicc2 iccss2
        mp3an sseli csbex ifex hbif breq1 ifbieq12d biidd ov2gf mp3an3 fnoprab2
        rgen2 wfn ssid xpss12 fnssres eqfnov2 mpbir ) KMEUHUIZFUJZUKZLULZUEUMZU
        FUMZYTUIZUUBUUCLUIZULZUFFUNUEYRUNZUUFUEUFYRFUUBYRVCZUUCFVCZUOZUUDUUBUUC
        KUIZUUEUUBUUCYRFKUPUUJUUBMUQURZBUUCAUUBIUSZUSZBUUCAUUBJUSZUSZUTZUUPUUKU
        UEUUHUUBVAVCZUULVBZMUUBULZVDZUOZUUIUUQUUPULZUUHUURMUUBUQURZUUBEUQURZVEZ
        UVBMVAVCZEVAVCZUUHUVFVFMDEUHUIZVCZUVGSUVJUVGDMUQURZMEUQURZDVAVCZUVHUVJU
        VGUVKUVLVEVFNODEMVGVHVIVJZOMEUUBVGVHUURUVDUVBUVEUURUVDUVAUURUVDUVAUVGUU
        RUVDUVAVFUVNUVGUURUOZUVDMUUBVPURZUUTVDUVAMUUBVKUVOUVPUUSUUTMUUBVLVMVNVO
        VQVRVSVTUUIUVBUVCUUIUURUVAUVCUUIUURUOZUUSUVCUUTUUSUVCUVQUULUUNUUPWAWBUV
        QUUTUOUUQUUNUUPUURUUTUUQUUNULZUUIUURUUTUOUULUVRUURUUTUULUURUULUUTUUBUUB
        UQURUUBWCMUUBUUBUQWDWEWFUULUUNUUPWGWHWIUUIUUTUUNUUPULUURUUIUUTUOBUUCGUS
        ZBUUCHUSZUUNUUPUUIUVSUVTULZUUTBUMZFVCZGHULZWRUUIUWAWRBUFUUIUWABUUIBWJBU
        GUGUVSUVTBUGUUCGUFWKZUGUMZUUCVCZBWJZWLBUGUUCHUWEUWHWLWMWNUWBUUCULZUWCUU
        IUWDUWAUWBUUCFWOUWIGUVSHUVTBUUCGWPBUUCHWPWQWSUCWTXAUUTUUNUVSULZUUIUWJUU
        BMUUBMULZUUCXBVCZUWJUWEUWKBUUCUUMGXBUWKUUMAMIUSGAUUBMIXCAUGMIGMUVISXDZU
        WFGVCAWJUAXEXFXGXIXJWBUUTUUPUVTULZUUIUWNUUBMUWKUWLUWNUWEUWKBUUCUUOHXBUW
        KUUOAMJUSHAUUBMJXCAUGMJHUWMUWFHVCAWJUBXEXFXGXIXJWBXHXKXLXMXNXOXPUUHUUBU
        VIVCZUUIUUKUUQULZYRUVIUUBUVJEUVIVCZYRUVIXQZSUVMUVHDEUQURUWQNOPDEXRXTUVM
        UVHUVJUWQUOUWRWRNODEMEXSVHVHZYAUWOUUIUUQXBVCUWPUULUUNUUPBUUCUUMUWEAUUBI
        UEWKZQYBYBBUUCUUOUWEAUUBJUWTRYBYBZYCABCUGUUBUUCUVIFAUMZMUQURZIJUTZUUQKU
        ULUUMUUOUTXBUWFUUBVCZAWJZUXEBWJZUXECWJZUWHUWGCWJZUULAUGUUMUUOUULAWJAUGU
        UBIUWTUXFWLAUGUUBJUWTUXFWLZYDUULBUGUUNUUPUULBWJBUGUUCUUMUWEUWHWLBUGUUCU
        UOUWEUWHWLZYDUXBUUBULUXCUULIJUUMUUOUXBUUBMUQYEAUUBIWPAUUBJWPZYFUWIUULUU
        LUUMUUOUUNUUPUWIUULYGBUUCUUMWPBUUCUUOWPZYFTYHYIXPUUHUUIUUPXBVCUUEUUPULU
        XAABCUGUUBUUCYRFJUUPLUUOXBUXFUXGUXHUWHUXIUXJUXKUXLUXMUDYHYIXHXLYKYTYSYL
        ZLYSYLUUAUUGVFKUVIFUJZYLYSUXOXQZUXNABCUVIFUXDKUXCIJQRYCTYJUWRFFXQUXPUWS
        FYMYRUVIFFYNVHUXOYSKYOVHABCYRFJLRUDYJUEUFYRFYTLYPVHYQ $.
        $( [19-Dec-2013] $) $( [11-Jun-2010] $)
    $}
  $}

