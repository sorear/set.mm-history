$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ring homomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c RngHom $.
  $c RngIso $.
  $c ~=r $.

  $( Extend class notation with the class of ring homomorphisms. $)
  crnghom $a class RngHom $.

  $( Extend class notation with the class of ring isomorphisms. $)
  crngiso $a class RngIso $.

  $( Extend class notation with the ring isomorphism relation. $)
  crisc $a class ~=r $.

  ${
    $d r s t f x y $.
    $( Define the function which gives the set of ring homomorphisms between
       two given rings. $)
    df-rnghom $a |- RngHom = { <. <. r , s >. , t >. | ( ( r e. RingOps /\
         s e. RingOps ) /\ t = { f | ( f : ran ( 1st ` r ) --> ran ( 1st ` s )
                  /\ ( f ` ( Id ` ( 2nd ` r ) ) ) = ( Id ` ( 2nd ` s ) )
                    /\ A. x e. ran ( 1st ` r ) A. y e. ran ( 1st ` r )
            ( ( f ` ( x ( 1st ` r ) y ) ) = ( ( f ` x ) ( 1st ` s ) ( f ` y ) )
          /\ ( f ` ( x ( 2nd ` r ) y ) ) = ( ( f ` x ) ( 2nd ` s ) ( f ` y ) )
                                                          ) ) } ) } $.
  $}

  ${
    $d R r s t f x y $.  $d S r s t f x y $.  $d X r s t f x y $.
    $d Y s t f y $.  $d G r s t $.  $d H r s t $.  $d J s t $.  $d K s t $.
    $d U r s t $.  $d V s t $.
    rnghomval.1 $e |- G = ( 1st ` R ) $.
    rnghomval.2 $e |- H = ( 2nd ` R ) $.
    rnghomval.3 $e |- X = ran G $.
    rnghomval.4 $e |- U = ( Id ` H ) $.
    rnghomval.5 $e |- J = ( 1st ` S ) $.
    rnghomval.6 $e |- K = ( 2nd ` S ) $.
    rnghomval.7 $e |- Y = ran J $.
    rnghomval.8 $e |- V = ( Id ` K ) $.
    $( The set of ring homomorphisms.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    rnghomval $p |- ( ( R e. RingOps /\ S e. RingOps ) -> ( R RngHom S ) =
            { f | ( f : X --> Y /\ ( f ` U ) = V /\ A. x e. X A. y e. X
              ( ( f ` ( x G y ) ) = ( ( f ` x ) J ( f ` y ) ) /\
            ( f ` ( x H y ) ) = ( ( f ` x ) K ( f ` y ) ) ) ) } ) $=
      ( vr vs vt cring cv c1st cfv crn wf c2nd cgi wceq co wral w3a cab crnghom
      wa cvv wcel fvex eqeltri rnex mapex mp2an simp1 ssexi fveq2 syl6eqr rneqd
      ss2abi feq2d fveq2d eqeq1d opreqd anbi12d raleqbidv 3anbi123d abbidv feq3
      wb syl eqeq2d 2ralbidv df-rnghom oprabval2 ) UBUCUDCDUEUEUBUFZUGUHZUIZUCU
      FZUGUHZUIZFUFZUJZWHUKUHZULUHZWNUHZWKUKUHZULUHZUMZAUFZBUFZWIUNZWNUHZXBWNUH
      ZXCWNUHZWLUNZUMZXBXCWPUNZWNUHZXFXGWSUNZUMZUSZBWJUOZAWJUOZUPZFUQLMWNUJZEWN
      UHZKUMZXBXCGUNZWNUHZXFXGIUNZUMZXBXCHUNZWNUHZXFXGJUNZUMZUSZBLUOALUOZUPZFUQ
      ZURLWMWNUJZXSWTUMZYBXHUMZYFXLUMZUSZBLUOZALUOZUPZFUQYLXRFUQZLUTVAMUTVAUUAU
      TVALGUIZUTPGGCUGUHZUTNCUGVBVCVDVCMIUIZUTTIIDUGUHZUTRDUGVBVCVDVCLMUTUTFVEV
      FYKXRFXRXTYJVGVLVHWHCUMZXQYTFUUFWOYMXAYNXPYSUUFWJLWMWNUUFWJUUBLUUFWIGUUFW
      IUUCGWHCUGVINVJZVKPVJZVMUUFWRXSWTUUFWQEWNUUFWQHULUHEUUFWPHULUUFWPCUKUHHWH
      CUKVIOVJZVNQVJVNVOUUFXOYRAWJLUUHUUFXNYQBWJLUUHUUFXIYOXMYPUUFXEYBXHUUFXDYA
      WNUUFWIGXBXCUUGVPVNVOUUFXKYFXLUUFXJYEWNUUFWPHXBXCUUIVPVNVOVQVRVRVSVTWKDUM
      ZYTYKFUUJYMXRYNXTYSYJUUJWMMUMYMXRWBUUJWMUUDMUUJWLIUUJWLUUEIWKDUGVIRVJZVKT
      VJWMMLWNWAWCUUJWTKXSUUJWTJULUHKUUJWSJULUUJWSDUKUHJWKDUKVISVJZVNUAVJWDUUJY
      QYIABLLUUJYOYDYPYHUUJXHYCYBUUJWLIXFXGUUKVPWDUUJXLYGYFUUJWSJXFXGUULVPWDVQW
      EVSVTABUDFUCUBWFWG $.
      $( [19-Jun-2010] $)

    $d F x y f $.  $d G f $.  $d H f $.  $d J f $.  $d K f $.  $d U f $.
    $d V f $.
    $( The predicate "is a ring homomorphism from ` R ` to ` S ` ."
       (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    isrnghom $p |- ( ( R e. RingOps /\ S e. RingOps )
       -> ( F e. ( R RngHom S ) <->
                ( F : X --> Y /\ ( F ` U ) = V /\ A. x e. X A. y e. X
              ( ( F ` ( x G y ) ) = ( ( F ` x ) J ( F ` y ) ) /\
            ( F ` ( x H y ) ) = ( ( F ` x ) K ( F ` y ) ) ) ) ) ) $=
      ( vf cring wcel wa crnghom co cv wf cfv wceq w3a cab rnghomval eleq2d cvv
      wral crn c1st fvex eqeltri rnex mpan2 3ad2ant1 feq1 fveq1 eqeq1d opreq12d
      fex eqeq12d anbi12d 2ralbidv 3anbi123d elab3 syl6bb ) CUCUDDUCUDUEZFCDUFU
      GZUDFLMUBUHZUIZEVRUJZKUKZAUHZBUHZGUGZVRUJZWBVRUJZWCVRUJZIUGZUKZWBWCHUGZVR
      UJZWFWGJUGZUKZUEZBLUQALUQZULZUBUMZUDLMFUIZEFUJZKUKZWDFUJZWBFUJZWCFUJZIUGZ
      UKZWJFUJZXBXCJUGZUKZUEZBLUQALUQZULZVPVQWQFABCDEUBGHIJKLMNOPQRSTUAUNUOWPXK
      UBFWRWTFUPUDZXJWRLUPUDXLLGURUPPGGCUSUJUPNCUSUTVAVBVALMUPFVIVCVDVRFUKZVSWR
      WAWTWOXJLMVRFVEXMVTWSKEVRFVFVGXMWNXIABLLXMWIXEWMXHXMWEXAWHXDWDVRFVFXMWFXB
      WGXCIWBVRFVFZWCVRFVFZVHVJXMWKXFWLXGWJVRFVFXMWFXBWGXCJXNXOVHVJVKVLVMVNVO
      $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d X x y $.  $d Y y $.  $d F x y $.
    rnghomf.1 $e |- G = ( 1st ` R ) $.
    rnghomf.2 $e |- X = ran G $.
    rnghomf.3 $e |- J = ( 1st ` S ) $.
    rnghomf.4 $e |- Y = ran J $.
    $( A ring homomorphism is a function.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    rnghomf $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                                          -> F : X --> Y ) $=
      ( vx vy cring wcel co wa cfv wceq eqid crnghom c2nd cgi wral w3a isrnghom
      wf cv biimpa simp1d 3impa ) ANOZBNOZCABUAPOZFGCUGZULUMQZUNQUOAUBRZUCRZCRB
      UBRZUCRZSZLUHZMUHZDPCRVBCRZVCCRZEPSVBVCUQPCRVDVEUSPSQMFUDLFUDZUPUNUOVAVFU
      ELMABURCDUQEUSUTFGHUQTIURTJUSTKUTTUFUIUJUK $.
      $( [19-Jun-2010] $)

    $( Closure law for a ring homomorphism.  (Contributed by Jeff Madsen,
       3-Jan-2011.) $)
    rnghomcl $p |- ( ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                          /\ A e. X ) -> ( F ` A ) e. Y ) $=
      ( cring wcel crnghom co w3a wf cfv rnghomf ffvelrn sylan ) BMNCMNDBCOPNQG
      HDRAGNADSHNBCDEFGHIJKLTGHADUAUB $.
      $( [3-Jan-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d F x y $.
    rnghom1.1 $e |- H = ( 2nd ` R ) $.
    rnghom1.2 $e |- U = ( Id ` H ) $.
    rnghom1.3 $e |- K = ( 2nd ` S ) $.
    rnghom1.4 $e |- V = ( Id ` K ) $.
    $( A ring homomorphism preserves ` 1 ` .  (Contributed by Jeff Madsen,
       24-Jun-2011.) $)
    rnghom1 $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                                  -> ( F ` U ) = V ) $=
      ( vx vy cring wcel co cfv wceq wa eqid crnghom c1st crn wral w3a isrnghom
      wf cv biimpa simp2d 3impa ) ANOZBNOZDABUAPOZCDQGRZULUMSZUNSAUBQZUCZBUBQZU
      CZDUGZUOLUHZMUHZUQPDQVBDQZVCDQZUSPRVBVCEPDQVDVEFPRSMURUDLURUDZUPUNVAUOVFU
      ELMABCDUQEUSFGURUTUQTHURTIUSTJUTTKUFUIUJUK $.
      $( [24-Jun-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d F x y $.  $d G x y $.  $d J x y $.
    $d X x y $.  $d A x y $.  $d B y $.
    rnghomadd.1 $e |- G = ( 1st ` R ) $.
    rnghomadd.2 $e |- X = ran G $.
    rnghomadd.3 $e |- J = ( 1st ` S ) $.
    $( Ring homomorphisms preserve addition.  (Contributed by Jeff Madsen,
       3-Jan-2011.) $)
    rnghomadd $p |- ( ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
         /\ ( A e. X /\ B e. X ) ) ->
                      ( F ` ( A G B ) ) = ( ( F ` A ) J ( F ` B ) ) ) $=
      ( vx vy wcel co cfv wceq wral wa eqid cring crnghom w3a c2nd crn isrnghom
      cv cgi biimpa simp3d 3impa simpl ralimi syl opreq1 fveq2d opreq1d eqeq12d
      wf fveq2 opreq2 opreq2d rcla42v impcom sylan ) CUANZDUANZECDUBONZUCZLUGZM
      UGZFOZEPZVJEPZVKEPZGOZQZMHRZLHRZAHNBHNSZABFOZEPZAEPZBEPZGOZQZVIVQVJVKCUDP
      ZOEPVNVODUDPZOQZSZMHRZLHRZVSVFVGVHWLVFVGSZVHSHGUEZEUSZWGUHPZEPWHUHPZQZWLW
      MVHWOWRWLUCLMCDWPEFWGGWHWQHWNIWGTJWPTKWHTWNTWQTUFUIUJUKWKVRLHWJVQMHVQWIUL
      UMUMUNVTVSWFVQWFAVKFOZEPZWCVOGOZQLMABHHVJAQZVMWTVPXAXBVLWSEVJAVKFUOUPXBVN
      WCVOGVJAEUTUQURVKBQZWTWBXAWEXCWSWAEVKBAFVAUPXCVOWDWCGVKBEUTVBURVCVDVE $.
      $( [3-Jan-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d F x y $.  $d H x y $.  $d K x y $.
    $d X x y $.  $d A x y $.  $d B y $.
    rnghommul.1 $e |- G = ( 1st ` R ) $.
    rnghommul.2 $e |- X = ran G $.
    rnghommul.3 $e |- H = ( 2nd ` R ) $.
    rnghommul.4 $e |- K = ( 2nd ` S ) $.
    $( Ring homomorphisms preserve multiplication.  (Contributed by Jeff
       Madsen, 3-Jan-2011.) $)
    rnghommul $p |- ( ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
        /\ ( A e. X /\ B e. X ) ) ->
                      ( F ` ( A H B ) ) = ( ( F ` A ) K ( F ` B ) ) ) $=
      ( vx vy wcel co cfv wceq wral cring crnghom w3a cv wa crn wf cgi isrnghom
      c1st biimpa simp3d 3impa simpr ralimi opreq1 fveq2d fveq2 opreq1d eqeq12d
      eqid syl opreq2 opreq2d rcla42v impcom sylan ) CUAPZDUAPZECDUBQPZUCZNUDZO
      UDZGQZERZVLERZVMERZHQZSZOITZNITZAIPBIPUEZABGQZERZAERZBERZHQZSZVKVLVMFQERV
      PVQDUJRZQSZVSUEZOITZNITZWAVHVIVJWMVHVIUEZVJUEIWIUFZEUGZGUHRZERHUHRZSZWMWN
      VJWPWSWMUCNOCDWQEFGWIHWRIWOJLKWQVAWIVAMWOVAWRVAUIUKULUMWLVTNIWKVSOIWJVSUN
      UOUOVBWBWAWHVSWHAVMGQZERZWEVQHQZSNOABIIVLASZVOXAVRXBXCVNWTEVLAVMGUPUQXCVP
      WEVQHVLAEURUSUTVMBSZXAWDXBWGXDWTWCEVMBAGVCUQXDVQWFWEHVMBEURVDUTVEVFVG $.
      $( [3-Jan-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d G x y $.  $d J x y $.  $d F x y $.
    rnggrphom.1 $e |- G = ( 1st ` R ) $.
    rnggrphom.2 $e |- J = ( 1st ` S ) $.
    $( A ring homomorphism is a group homomorphism.  (Contributed by Jeff
       Madsen, 2-Jan-2011.) $)
    rnggrphom $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                                  -> F e. ( G GrpHom J ) ) $=
      ( vx vy cring wcel co crn cv cfv wral wa cgr ringogrpo eqid crnghom cghom
      wf wb elghom syl2an 3adant3 rnghomf rnghomadd eqcomd ralrimivva mpbir2and
      w3a wceq ) AJKZBJKZCABUALKZUMZCDEUBLKZDMZEMZCUCZHNZCOINZCOELZVCVDDLCOZUNZ
      IUTPHUTPZUOUPUSVBVHQUDZUQUODRKERKVIUPADFSBEGSHICDEVAUTUTTZVATZUEUFUGABCDE
      UTVAFVJGVKUHURVGHIUTUTURVCUTKVDUTKQQVFVEVCVDABCDEUTFVJGUIUJUKUL $.
      $( [2-Jan-2011] $)
  $}

  ${
    rnghom0.1 $e |- G = ( 1st ` R ) $.
    rnghom0.2 $e |- Z = ( Id ` G ) $.
    rnghom0.3 $e |- J = ( 1st ` S ) $.
    rnghom0.4 $e |- W = ( Id ` J ) $.
    $( A ring homomorphism preserves ` 0 ` .  (Contributed by Jeff Madsen,
       2-Jan-2011.) $)
    rnghom0 $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                                      -> ( F ` Z ) = W ) $=
      ( cring wcel crnghom co w3a cgr cghom cfv ringogrpo wceq rnggrphom ghomid
      3ad2ant1 3ad2ant2 syl111anc ) ALMZBLMZCABNOMZPDQMZEQMZCDEROMGCSFUAUGUHUJU
      IADHTUDUHUGUKUIBEJTUEABCDEHJUBFGCDEIKUCUF $.
      $( [2-Jan-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d F x y $.  $d H x y $.  $d K x y $.
    $d X x y $.  $d A x y $.  $d B y $.
    rnghomsub.1 $e |- G = ( 1st ` R ) $.
    rnghomsub.2 $e |- X = ran G $.
    rnghomsub.3 $e |- H = ( /g ` G ) $.
    rnghomsub.4 $e |- J = ( 1st ` S ) $.
    rnghomsub.5 $e |- K = ( /g ` J ) $.
    $( Ring homomorphisms preserve subtraction.  (Contributed by Jeff Madsen,
       15-Jun-2011.) $)
    rnghomsub $p |- ( ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
           /\ ( A e. X /\ B e. X ) ) ->
                      ( F ` ( A H B ) ) = ( ( F ` A ) K ( F ` B ) ) ) $=
      ( cring wcel co w3a cfv crnghom cghom wa wceq ringogrpo 3ad2ant1 3ad2ant2
      cgr rnggrphom 3jca ghomdiv sylan ) CPQZDPQZECDUARQZSZFUHQZHUHQZEFHUBRQZSA
      JQBJQUCABGRETAETBETIRUDUPUQURUSUMUNUQUOCFKUEUFUNUMURUODHNUEUGCDEFHKNUIUJA
      BIGEFHJLMOUKUL $.
      $( [15-Jun-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d T x y $.  $d F x y $.  $d G x y $.
    $( The composition of two ring homomorphisms is a ring homomorphism.
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    rnghomco $p |- ( ( ( R e. RingOps /\ S e. RingOps /\ T e. RingOps ) /\
                          ( F e. ( R RngHom S ) /\ G e. ( S RngHom T ) ) )
                                      -> ( G o. F ) e. ( R RngHom T ) ) $=
      ( vx vy wcel w3a co wa cfv wceq eqid 3expa 3adantl3 fvco3 wi ex imp cring
      crnghom ccom c1st crn wf c2nd cgi cv rnghomf 3adantl1 adantrl adantrr fco
      wral syl11anc wfun ffun syl ring1cl 3ad2ant1 adantr rnghom1 fveq2d 3eqtrd
      syl111anc rnghomadd adantlrr rnghomcl anim12da syldan eqtrd ringgcl 3expb
      adantlrl 3ad2antl1 adantlr jca sylan opreq12 3eqtr4d rnghommul ralrimivva
      ringocl 3jca wb isrnghom 3adant2 mpbird ) AUAHZBUAHZCUAHZIZDABUBJHZEBCUBJ
      HZKZKZEDUCZACUBJHZAUDLZUEZCUDLZUEZWRUFZAUGLZUHLZWRLZCUGLZUHLZMZFUIZGUIZWT
      JZWRLZXKWRLZXLWRLZXBJZMZXKXLXEJZWRLZXOXPXHJZMZKZGXAUOFXAUOZIZWQXDXJYDWQBU
      DLZUEZXCEUFZXAYGDUFZXDWMWOYHWNWKWLWOYHWJWKWLWOYHBCEYFXBYGXCYFNZYGNZXBNZXC
      NZUJOUKULZWMWNYIWOWJWKWNYIWLWJWKWNYIABDWTYFXAYGWTNZXANZYJYKUJOPUMZXAYGXCE
      DUNUPWQXGXFDLZELZBUGLZUHLZELZXIWQEUQZYIXFXAHZXGYSMWQYHUUCYNYGXCEURUSZYQWM
      UUDWPWJWKUUDWLAXFXEXAYPXENZXFNZUTVAVBXAYGXFEDQVFWQYRUUAEWMWNYRUUAMZWOWJWK
      WNUUHWLWJWKWNUUHABXFDXEYTUUAUUFUUGYTNZUUANZVCOPUMVDWMWOUUBXIMZWNWKWLWOUUK
      WJWKWLWOUUKBCUUAEYTXHXIUUIUUJXHNZXINZVCOUKULVEWQYCFGXAXAWQXKXAHZXLXAHZKZK
      ZXRYBUUQXMDLZELZXKDLZELZXLDLZELZXBJZXNXQUUQUUSUUTUVBYFJZELZUVDUUQUURUVEEW
      MWNUUPUURUVEMZWOWMWNKZUUPUVGWJWKWNUUPUVGRZWLWJWKWNUVIWJWKWNIZUUPUVGXKXLAB
      DWTYFXAYOYPYJVGSOPTVHVDWQUUPUUTYGHZUVBYGHZKZUVFUVDMZWMWNUUPUVMWOUVHUUPUVM
      WJWKWNUUPUVMRZWLWJWKWNUVOUVJUUPUVMUVJUUNUUOUVKUVLXKABDWTYFXAYGYOYPYJYKVIX
      LABDWTYFXAYGYOYPYJYKVIVJSOPTVHZWMWOUVMUVNWNWMWOKZUVMUVNWKWLWOUVMUVNRZWJWK
      WLWOUVRWKWLWOIZUVMUVNUUTUVBBCEYFXBYGYJYKYLVGSOUKTVOVKVLWQUUPXMXAHZXNUUSMZ
      WMUUPUVTWPWJWKUUPUVTWLWJUUNUUOUVTXKXLAWTXAYOYPVMVNVPVQWQUUCYIKZUVTUWAWQUU
      CYIUUEYQVRZUUCYIUVTUWAXAYGXMEDQOVSVKUUQXOUVAMZXPUVCMZKZXQUVDMWQUUNUUOUWDU
      WEWQUWBUUNUWDUWCUUCYIUUNUWDXAYGXKEDQOVSWQUWBUUOUWEUWCUUCYIUUOUWEXAYGXLEDQ
      OVSVJZXOUVAXPUVCXBVTUSWAUUQXSDLZELZUVAUVCXHJZXTYAUUQUWIUUTUVBYTJZELZUWJUU
      QUWHUWKEWMWNUUPUWHUWKMZWOUVHUUPUWMWJWKWNUUPUWMRZWLWJWKWNUWNUVJUUPUWMXKXLA
      BDWTXEYTXAYOYPUUFUUIWBSOPTVHVDWQUUPUVMUWLUWJMZUVPWMWOUVMUWOWNUVQUVMUWOWKW
      LWOUVMUWORZWJWKWLWOUWPUVSUVMUWOUUTUVBBCEYFYTXHYGYJYKUUIUULWBSOUKTVOVKVLWQ
      UUPXSXAHZXTUWIMZWMUUPUWQWPWJWKUUPUWQWLWJUUNUUOUWQXKXLAWTXEXAYOUUFYPWDVNVP
      VQWQUWBUWQUWRUWCUUCYIUWQUWRXAYGXSEDQOVSVKUUQUWFYAUWJMUWGXOUVAXPUVCXHVTUSW
      AVRWCWEWMWSYEWFZWPWJWLUWSWKFGACXFWRWTXEXBXHXIXAXCYOUUFYPUUGYLUULYMUUMWGWH
      VBWI $.
      $( [16-Jun-2011] $)
  $}

  ${
    rngkerinj.1 $e |- G = ( 1st ` R ) $.
    rngkerinj.2 $e |- X = ran G $.
    rngkerinj.3 $e |- W = ( Id ` G ) $.
    rngkerinj.4 $e |- J = ( 1st ` S ) $.
    rngkerinj.5 $e |- Y = ran J $.
    rngkerinj.6 $e |- Z = ( Id ` J ) $.
    $( A ring homomorphism is injective if and only if its kernel is zero.
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    rngkerinj $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
        -> ( F : X -1-1-> Y <-> ( `' F " { Z } ) = { W } ) ) $=
      ( wcel cfv crn eqtri cgi cring crnghom co w3a c1st cgr cghom wf1 ccnv csn
      cima wceq wb eqid ringogrpo 3ad2ant1 3ad2ant2 rnggrphom fveq2i grpokerinj
      rneqi syl111anc ) AUAPZBUAPZCABUBUCPZUDAUEQZUFPZBUEQZUFPZCVFVHUGUCPGHCUHC
      UIIUJUKFUJULUMVCVDVGVEAVFVFUNZUOUPVDVCVIVEBVHVHUNZUOUQABCVFVHVJVKURICVFVH
      FGHGDRVFRKDVFJVASFDTQVFTQLDVFTJUSSHERVHRNEVHMVASIETQVHTQOEVHTMUSSUTVB $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d r s t f $.
    $( Define the function which gives the set of ring isomorphisms between two
       given rings. $)
    df-rngiso $a |- RngIso = { <. <. r , s >. , t >. |
              ( ( r e. RingOps /\ s e. RingOps ) /\ t = { f e. ( r RngHom s ) |
                  f : ran ( 1st ` r ) -1-1-onto-> ran ( 1st ` s ) } ) } $.
  $}

  ${
    $d R r s t f $.  $d S r s t f $.  $d X r s t $.  $d Y s t $.
    rngisoval.1 $e |- G = ( 1st ` R ) $.
    rngisoval.2 $e |- X = ran G $.
    rngisoval.3 $e |- J = ( 1st ` S ) $.
    rngisoval.4 $e |- Y = ran J $.
    $( The set of ring isomorphisms.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    rngisoval $p |- ( ( R e. RingOps /\ S e. RingOps ) -> ( R RngIso S ) =
                          { f e. ( R RngHom S ) | f : X -1-1-onto-> Y } ) $=
      ( vr vs cv c1st cfv crn crnghom wceq syl6eqr vt cring wf1o co oprex rabex
      crab crngiso opreq1 wb fveq2 f1oeq2 syl rabeqbidv opreq2 f1oeq3 df-rngiso
      rneqd oprabval2 ) LMUAABUBUBLNZOPZQZMNZOPZQZCNZUCZCUTVCRUDZUGFGVFUCZCABRU
      DZUGUHFVEVFUCZCAVCRUDZUGVICVJABRUEUFUTASZVGVKCVHVLUTAVCRUIVMVBFSVGVKUJVMV
      BDQFVMVADVMVAAOPDUTAOUKHTURITVBFVEVFULUMUNVCBSZVKVICVLVJVCBARUOVNVEGSVKVI
      UJVNVEEQGVNVDEVNVDBOPEVCBOUKJTURKTVEGFVFUPUMUNUACMLUQUS $.
      $( [16-Jun-2011] $)

    $d X f $.  $d Y f $.  $d F f $.
    $( The predicate "is a ring isomorphism between ` R ` and ` S ` ."
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    isrngiso $p |- ( ( R e. RingOps /\ S e. RingOps ) -> ( F e. ( R RngIso S )
      <-> ( F e. ( R RngHom S ) /\ F : X -1-1-onto-> Y ) ) ) $=
      ( vf cring wcel wa crngiso co cv wf1o crnghom crab rngisoval eleq2d elrab
      f1oeq1 syl6bb ) AMNBMNOZCABPQZNCFGLRZSZLABTQZUAZNCUKNFGCSZOUGUHULCABLDEFG
      HIJKUBUCUJUMLCUKFGUICUEUDUF $.
      $( [16-Jun-2011] $)

    $( A ring isomorphism is a bijection.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    rngiso1o $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngIso S ) )
                                          -> F : X -1-1-onto-> Y ) $=
      ( cring wcel crngiso co wf1o wa crnghom isrngiso simplbda 3impa ) ALMZBLM
      ZCABNOMZFGCPZUBUCQUDCABROMUEABCDEFGHIJKSTUA $.
      $( [16-Jun-2011] $)
  $}

  $( A ring isomorphism is a ring homomorphism.  (Contributed by Jeff Madsen,
     16-Jun-2011.) $)
  rngisohom $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngIso S ) )
                                          -> F e. ( R RngHom S ) ) $=
    ( cring wcel crngiso co crnghom wa c1st cfv crn wf1o eqid isrngiso simprbda
    3impa ) ADEZBDEZCABFGEZCABHGEZRSITUAAJKZLZBJKZLZCMABCUBUDUCUEUBNUCNUDNUENOP
    Q $.
    $( [16-Jun-2011] $)

  ${
    $d R x y $.  $d S x y $.  $d F x y $.
    $( The inverse of a ring isomorphism is a ring isomorphism.  (Contributed
       by Jeff Madsen, 16-Jun-2011.) $)
    rngisocnv $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngIso S ) )
                                          -> `' F e. ( S RngIso R ) ) $=
      ( vx vy wcel co wa cfv wceq eqid sylan2 ancoms f1ocnvfv2 adantll f1ocnvdm
      wi 3expb sylan an32s cring crngiso ccnv crnghom c1st crn wf1o wf c2nd cgi
      wral w3a f1ocnv f1of syl ad2antll rnghom1 3expa adantrr ring1cl ad2ant2rl
      cv f1ocnvfv mpd anim12da opreq12 rnghomadd impr ringgcl adantlll adantlrl
      exp32 imp 3eqtr4rd wb wf1 f1of1 ad2antlr anassrs adantllr f1fveq syl12anc
      mpbid rnghommul ringocl jca ralrimivva isrnghom adantr mpbird ex isrngiso
      3jca 3imtr4d 3impia ) AUAFZBUAFZCABUBGFZCUCZBAUBGFZWPWQHZCABUDGFZAUEIZUFZ
      BUEIZUFZCUGZHZWSBAUDGFZXFXDWSUGZHZWRWTXAXHXKXAXHHZXIXJXLXIXFXDWSUHZBUIIZU
      JIZWSIAUIIZUJIZJZDVBZEVBZXEGZWSIZXSWSIZXTWSIZXCGZJZXSXTXNGZWSIZYCYDXPGZJZ
      HZEXFUKDXFUKZULZXLXMXRYLXGXMXAXBXGXJXMXDXFCUMZXFXDWSUNUOUPXLXQCIXOJZXRXAX
      BYOXGWPWQXBYOABXQCXPXNXOXPKZXQKZXNKZXOKZUQURUSWPXGYOXRQZWQXBXGWPYTWPXGXQX
      DFYTAXQXPXDXDKZYPYQUTXDXFXQXOCVCLMVAVDXLYKDEXFXFXLXSXFFZXTXFFZHZHZYFYJUUE
      YBCIZYECIZJZYFUUEYCCIZYDCIZXEGZYAUUGUUFXHUUDUUKYAJZXAXGUUDUULXBXGUUDHZUUI
      XSJZUUJXTJZHZUULXGUUBUUCUUNUUOXDXFXSCNXDXFXTCNVEZUUIXSUUJXTXEVFUOOOXLUUDU
      UGUUKJZXAXBXGUUDUURQZWPWQXBXGUUSQWPWQXBULZXGUUDUURUUMUUTYCXDFZYDXDFZHZUUR
      XGUUBUUCUVAUVBXDXFXSCPXDXFXTCPVEZYCYDABCXCXEXDXCKZUUAXEKZVGLVLURVHVMXAXGU
      UDUUFYAJZXBWQXGUUDUVGWPWQUUDXGUVGWQUUDHZYAXFFZXGUVGWQUUBUUCUVIXSXTBXEXFUV
      FXFKZVIRZXGUVIUVGXDXFYACNMSTVJVKVNXAXGUUDUUHYFVOZXBXAXGHUUDHZXDXFCVPZYBXD
      FZYEXDFZUVLXGUVNXAUUDXDXFCVQVRZWQXGUUDUVOWPWQUUDXGUVOUVHUVIXGUVOUVKXGUVIU
      VOXDXFYACPMSTVJWPXGUUDUVPWQWPXGUUDUVPUUMWPUVCUVPUVDWPUVAUVBUVPYCYDAXCXDUV
      EUUAVIRLVSVTXDXFYBYECWAWBVKWCUUEYHCIZYICIZJZYJUUEUUIUUJXNGZYGUVSUVRXHUUDU
      WAYGJZXAXGUUDUWBXBUUMUUPUWBUUQUUIXSUUJXTXNVFUOOOXLUUDUVSUWAJZXAXBXGUUDUWC
      QZWPWQXBXGUWDQUUTXGUUDUWCUUMUUTUVCUWCUVDYCYDABCXCXPXNXDUVEUUAYPYRWDLVLURV
      HVMXAXGUUDUVRYGJZXBWQXGUUDUWEWPWQUUDXGUWEUVHYGXFFZXGUWEWQUUBUUCUWFXSXTBXE
      XNXFUVFYRUVJWERZXGUWFUWEXDXFYGCNMSTVJVKVNXAXGUUDUVTYJVOZXBUVMUVNYHXDFZYIX
      DFZUWHUVQWQXGUUDUWIWPWQUUDXGUWIUVHUWFXGUWIUWGXGUWFUWIXDXFYGCPMSTVJWPXGUUD
      UWJWQWPXGUUDUWJUUMWPUVCUWJUVDWPUVAUVBUWJYCYDAXCXPXDUVEYPUUAWERLVSVTXDXFYH
      YICWAWBVKWCWFWGWMXAXIYMVOZXHWQWPUWKDEBAXOWSXEXNXCXPXQXFXDUVFYRUVJYSUVEYPU
      UAYQWHMWIWJXGXJXAXBYNUPWFWKABCXCXEXDXFUVEUUAUVFUVJWLWQWPWTXKVOBAWSXEXCXFX
      DUVFUVJUVEUUAWLMWNWO $.
      $( [16-Jun-2011] $)
  $}

  ${
    $( The composition of two ring isomorphisms is a ring isomorphism.
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    rngisoco $p |- ( ( ( R e. RingOps /\ S e. RingOps /\ T e. RingOps ) /\
                      ( F e. ( R RngIso S ) /\ G e. ( S RngIso T ) ) )
                                    -> ( G o. F ) e. ( R RngIso T ) ) $=
      ( cring wcel crngiso co wa crnghom c1st cfv crn wf1o eqid rngisohom 3expa
      3adantl3 3adantl1 w3a wb isrngiso 3adant2 adantr anim12da rnghomco syldan
      ccom rngiso1o adantrl adantrr f1oco syl11anc mpbir2and ) AFGZBFGZCFGZUAZD
      ABHIGZEBCHIGZJZJZEDUIZACHIGZVDACKIGZALMZNZCLMZNZVDOZUSVEVFVKJUBZVBUPURVLU
      QACVDVGVIVHVJVGPZVHPZVIPZVJPZUCUDUEUSVBDABKIGZEBCKIGZJVFUSUTVAVQVRUPUQUTV
      QURUPUQUTVQABDQRSUQURVAVRUPUQURVAVRBCEQRTUFABCDEUGUHVCBLMZNZVJEOZVHVTDOZV
      KUSVAWAUTUQURVAWAUPUQURVAWABCEVSVIVTVJVSPZVTPZVOVPUJRTUKUSUTWBVAUPUQUTWBU
      RUPUQUTWBABDVGVSVHVTVMVNWCWDUJRSULVHVTVJEDUMUNUO $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d r s f $.
    $( Define the ring isomorphism relation. $)
    df-risc $a |- ~=r = { <. r , s >. | ( ( r e. RingOps /\ s e. RingOps )
                                  /\ E. f f e. ( r RngIso s ) ) } $.
  $}

  ${
    $d R r s f $.  $d S r s f $.
    $( The ring isomorphism relation.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    isriscg $p |- ( ( R e. A /\ S e. B ) -> ( R ~=r S <->
        ( ( R e. RingOps /\ S e. RingOps ) /\ E. f f e. ( R RngIso S ) ) ) ) $=
      ( vr vs cv cring wcel wa crngiso co wex crisc eleq1 eleq2d exbidv anbi12d
      wceq anbi1d opreq1 anbi2d opreq2 df-risc brabg ) FHZIJZGHZIJZKZEHZUGUILMZ
      JZENZKCIJZUJKZULCUILMZJZENZKUPDIJZKZULCDLMZJZENZKFGCDABOUGCTZUKUQUOUTVFUH
      UPUJUGCIPUAVFUNUSEVFUMURULUGCUILUBQRSUIDTZUQVBUTVEVGUJVAUPUIDIPUCVGUSVDEV
      GURVCULUIDCLUDQRSEGFUEUF $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d R f $.  $d S f $.
    isrisc.1 $e |- R e. _V $.
    isrisc.2 $e |- S e. _V $.
    $( The ring isomorphism relation.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    isrisc $p |- ( R ~=r S <-> ( ( R e. RingOps /\ S e. RingOps )
                              /\ E. f f e. ( R RngIso S ) ) ) $=
      ( cvv wcel crisc wbr cring wa cv crngiso co wex wb isriscg mp2an ) AFGBFG
      ABHIAJGBJGKCLABMNGCOKPDEFFABCQR $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d R f $.  $d S f $.
    $( The ring isomorphism relation.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    risc $p |- ( ( R e. RingOps /\ S e. RingOps ) ->
                      ( R ~=r S <-> E. f f e. ( R RngIso S ) ) ) $=
      ( cring wcel wa crisc wbr cv crngiso co wex isriscg bianabs ) ADEBDEFABGH
      CIABJKECLDDABCMN $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d R f $.  $d S f $.  $d F f $.
    $( Determine that two rings are isomorphic.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    risci $p |- ( ( R e. RingOps /\ S e. RingOps /\
                                F e. ( R RngIso S ) ) -> R ~=r S ) $=
      ( vf cring wcel crngiso co crisc wbr wa cv wex eleq1 cla4egv pm2.43i risc
      syl5ibr 3impia ) AEFZBEFZCABGHZFZABIJZUCUDTUAKDLZUBFZDMZUCUGUFUCDCUBUECUB
      NOPABDQRS $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d f g r s t $.
    $( Ring isomorphism is an equivalence relation.  (Contributed by Jeff
       Madsen, 16-Jun-2011.) $)
    riscer $p |- Er ~=r $=
      ( vr vs vt vf vg crisc cv wbr cring wcel crngiso wex visset isrisc 3expia
      wa co wi risci syld rngisocnv ancoms exlimdv imp sylbi w3a eeanv rngisoco
      ccnv ccom ex 3adant2 exlimdvv syl5bir 3expb adantlr an4s syl2anb ster ) A
      BCFAGZBGZFHZUTIJZVAIJZPZDGZUTVAKQJZDLZPZVAUTFHZUTVADAMBMZNZVEVHVJVEVGVJDV
      EVGVFUIZVAUTKQJZVJVCVDVGVNUTVAVFUAOVDVCVNVJRVDVCVNVJVAUTVMSOUBTUCUDUEVBVI
      VDCGZIJZPZEGZVAVOKQJZELZPUTVOFHZVAVOFHVLVAVOEVKCMNVEVQVHVTWAVEVQPVHVTPZWA
      VCVQWBWARZVDVCVDVPWCWBVGVSPZELDLVCVDVPUFZWAVGVSDEUGWEWDWADEWEWDVRVFUJZUTV
      OKQJZWAWEWDWGUTVAVOVFVRUHUKVCVPWGWARVDVCVPWGWAUTVOWFSOULTUMUNUOUPUDUQURUS
      $.
      $( [16-Jun-2011] $)
  $}

