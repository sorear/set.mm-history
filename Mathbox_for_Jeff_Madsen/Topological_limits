$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Topological limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ~~>t $.
  $( Extend class notation with the topological convergence relation. $)
  ctlm $a class ~~>t $.

  ${
    $d j r f x u m n $.
    $( Define the convergence relation on sequences in a topological space.  A
       sequence ` f ` converges to a point ` x ` if for every open neighborhood
       ` U ` of ` x ` , for all large enough ` m ` we have
       ` f ( m ) e. U ` . $)
    df-tlm $a |- ~~>t = { <. j , r >. | ( j e. Top /\ r = { <. f , x >. |
            ( f : NN --> U. j /\ x e. U. j /\ A. u e. ( ( nei ` j ) ` { x } )
                      E. n e. NN A. m e. ( ZZ>= ` n ) ( f ` m ) e. u ) } ) } $.
  $}

  ${
    $d J j r f x u m n $.  $d X j r f x u m n $.  $d F f x u m n $.
    $d Y f x u m n $.  $d A f x u m n $.
    tlmval.1 $e |- X = U. J $.
    $( The convergence relation on sequences in a topological space. $)
    tlmval $p |- ( J e. Top -> ( ~~>t ` J ) = { <. f , x >. |
                ( f : NN --> X /\ x e. X /\ A. u e. ( ( nei ` J ) ` { x } )
                      E. n e. NN A. m e. ( ZZ>= ` n ) ( f ` m ) e. u ) } ) $=
      ( vj vr ctop wcel cn cv wf cfv cuz wral wrex csn cnei w3a copab cvv ctlm
      wceq cxp cpw wss ssexg wa fssxp visset elpw sylibr anim1i 3impa ssopab2i
      opabssxp sstri xpexg nnex cuni uniexg syl5eqel sylancr pwexg syl
      syl11anc wb unieq syl6eqr feq3 eleq2d fveq2 fveq1d raleqdv 3anbi123d
      opabbidv df-tlm fvopab4g mpdan ) FKLZMGCNZOZANZGLZDNWDPBNLDENQPREMSZBWFTZ
      FUAPZPZRZUBZCAUCZUDLZFUEPWNUFWNMGUGZUHZGUGZUIWRUDLZWOWCWNWRUDUJWNWDWQLZWG
      UKZWLUKZCAUCWRWMXBCAWEWGWLXBWEWGUKXAWLWEWTWGWEWDWPUIWTMGWDULWDWPCUMUNUOUP
      UPUQURWLCAWQGUSUTWCWQUDLZGUDLZWSWCWPUDLZXCMUDLXDXEWCMGUDUDVAVBWCFVCZUDGFK
      VDHVEZVFWPUDVGVHXGWQGUDUDVAVIVFIJFMINZVCZWDOZWFXILZWHBWIXHUAPZPZRZUBZCAUC
      WNKUDUEXHFUFZXOWMCAXPXJWEXKWGXNWLXPXIGUFXJWEVJXPXIXFGXHFVKHVLZXIGMWDVMVHX
      PXIGWFXQVNXPWHBXMWKXPWIXLWJXHFUAVOVPVQVRVSABCIDEJVTWAWB $.
      $( [2-Sep-2009] $)

    $( The binary relation " ` F ` converges to ` Y ` topologically". $)
    tlmbr $p |- ( ( J e. Top /\ Y e. A ) -> ( F ( ~~>t ` J ) Y <->
          ( F : NN --> X /\ Y e. X /\ A. u e. ( ( nei ` J ) ` { Y } )
                    E. n e. NN A. m e. ( ZZ>= ` n ) ( F ` m ) e. u ) ) ) $=
      ( vf vx ctop wcel ctlm cfv wbr cn cv wf cuz wral wrex csn cnei w3a copab
      tlmval breqd cvv wceq feq1 fveq1 eleq1d rexralbidv ralbidv 3anbi13d
      eleq1 sneq fveq2d raleqdv 3anbi23d eqid nnex fex mpan2 3ad2ant1 brabg2
      sylan9bb ) FLMZEHFNOZPEHQGJRZSZKRZGMZCRZVKOZARZMZCDRTOZUADQUBZAVMUCZFUDOZ
      OZUAZUEZJKUFZPHBMQGESZHGMZVOEOZVQMZCVSUADQUBZAHUCZWBOZUAZUEZVIVJWFEHKAJCD
      FGIUGUHWEWGVNWKAWCUAZUEWOJKEHUIBWFVKEUJZVLWGWDWPVNQGVKEUKWQVTWKAWCWQVRWJD
      CQVSWQVPWIVQVOVKEULUMUNUOUPVMHUJZVNWHWPWNWGVMHGUQWRWKAWCWMWRWAWLWBVMHURUS
      UTVAWFVBWGWHEUIMZWNWGQUIMWSVCQGUIEVDVEVFVGVH $.
      $( [2-Sep-2009] $)

    $( The binary relation " ` F ` converges to ` Y ` topologically". $)
    tlmbrf $p |- ( ( J e. Top /\ Y e. A /\ F : NN --> X ) -> ( F ( ~~>t ` J ) Y
                    <-> ( Y e. X /\ A. u e. ( ( nei ` J ) ` { Y } ) E. n e. NN
                                A. m e. ( ZZ>= ` n ) ( F ` m ) e. u ) ) ) $=
      ( ctop wcel cn wf w3a ctlm cfv wbr cv cuz wral wrex csn cnei wa wb tlmbr
      3adant3 biidd 3anibar 3anass syl5bb bitrd ) FJKZHBKZLGEMZNZEHFOPQZUOHGKZC
      REPARKCDRSPTDLUAAHUBFUCPPTZNZURUSUDZUMUNUQUTUEUOABCDEFGHIUFUGUPUOVAUDZVAU
      TUMUNUOVBVAUPVBUHUIUOURUSUJUKUL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F a b c d p q u v x y $.  $d J a b c d p q u v x y $.
    $d X a b c d p q u v x y $.
    haustlmu.1 $e |- X = U. J $.
    $( Limits are unique in a Hausdorff space. $)
    haustlmu $p |- ( J e. Haus -> E* x F ( ~~>t ` J ) x ) $=
      ( vy vb vu va vd vv vc vp vq cha wcel cv ctlm cfv wbr wa wceq wi wal wmo
      cn wf cuz wral wrex csn cnei w3a ctop wb haustop cvv visset tlmbr mpan2
      anbi12d syl wne wn cin c0 hausnei opnneip 3expib anim12d adantr reeanv
      caddc co cn0 uzaddcl cz nnz uzid nnnn0 syl2an fveq2 eleq1d rcla4v cc
      addcom nncn ancoms eqeltrd inelcm syl6 r19.23aivv sylbir eleq2
      rexralbidv rcla4va an4s ex necon2bd an4 syl5ib expdimp exp3a 3impd
      r19.23advv mpd 3exp2 imp32 necon4ad expimpd 3simpc syl2ani sylbid
      19.21aivv breq2 mo4 sylibr ) COPZBAQZCRSZTZBFQZXTTZUAZXSYBUBZUCZFUDAUDYAA
      UEXRYFAFXRYDUFDBUGZXSDPZGQZBSZHQZPZGIQZUHSZUIIUFUJZHXSUKCULSZSZUIZUMZYGYB
      DPZJQZBSZKQZPZJLQZUHSZUILUFUJZKYBUKYPSZUIZUMZUAZYEXRCUNPZYDUUKUOCUPZUULYA
      YSYCUUJUULXSUQPYAYSUOAURHUQGIBCDXSEUSUTUULYBUQPYCUUJUOFURKUQJLBCDYBEUSUTV
      AVBXRYHYRUAZYTUUIUAZYEYSUUJXRYHYTUAZYRUUIUAZUAYEUUNUUOUAXRUUPUUQYEXRUUPUA
      UUQXSYBXRYHYTXSYBVCZUUQVDZUCXRYHYTUURUUSXRYHYTUURUMZUAZXSMQZPZYBNQZPZUVBU
      VDVEZVFUBZUMZNCUJMCUJUUSXSYBNMCDEVGUVAUVHUUSMNCCUVAUVBCPZUVDCPZUAZUVHUUSU
      CUVAUVKUAZUVCUVEUVGUUSUVLUVCUVEUVGUUSUCZUVAUVKUVCUVEUAZUVMUVAUVIUVCUAZUVJ
      UVEUAZUAZUVMUVKUVNUAUVAUVQUVBYQPZUVDUUHPZUAZUVMXRUVQUVTUCZUUTXRUULUWAUUMU
      ULUVOUVRUVPUVSUULUVIUVCUVRXSCUVBVHVIUULUVJUVEUVSYBCUVDVHVIVJVBVKUVTUUQUVF
      VFUVTUUQUVFVFVCZUVRYRUVSUUIUWBYJUVBPZGYNUIZIUFUJZUUBUVDPZJUUFUIZLUFUJZUWB
      UVRYRUAUVSUUIUAUWEUWHUAUWDUWGUAZLUFUJIUFUJUWBUWDUWGILUFUFVLUWIUWBILUFUFYM
      UFPZUUEUFPZUAZUWIYMUUEVMVNZBSZUVBPZUWNUVDPZUAUWBUWLUWDUWOUWGUWPUWLUWMYNPZ
      UWDUWOUCYMYNPZUUEVOPUWQUWJUWKUUEYMYMVPUWJYMVQPUWRYMVRYMVSVBUUEVTWAUWCUWOG
      UWMYNYIUWMUBYJUWNUVBYIUWMBWBWCWDVBUWLUWMUUFPUWGUWPUCUWLUWMUUEYMVMVNZUUFYM
      WEPUUEWEPUWMUWSUBUWJUWKYMUUEWFYMWGUUEWGWAUWKUWJUWSUUFPZUUEUUFPZYMVOPUWTUW
      KUWJYMUUEUUEVPUWKUUEVQPUXAUUEVRUUEVSVBYMVTWAWHWIUWFUWPJUWMUUFUUAUWMUBUUBU
      WNUVDUUAUWMBWBWCWDVBVJUWNUVBUVDWJWKWLWMYOUWEHUVBYQYKUVBUBYLUWCIGUFYNYKUVB
      YJWNWOWPUUGUWHKUVDUUHUUCUVDUBUUDUWFLJUFUUFUUCUVDUUBWNWOWPWAWQWRWSWKUVIUVJ
      UVCUVEWTXAXBXCXDWRXEXFXGXHXIXJYHYRYTUUIWTXAYGYHYRXKYGYTUUIXKXLXMXNYAYCAFX
      SYBBXTXOXPXQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J m n u $.  $d X m n u $.  $d Y m n u $.
    tlmconst.1 $e |- X = U. J $.
    $( A constant sequence converges to the constant. $)
    tlmconst $p |- ( ( J e. Top /\ Y e. X ) ->
                                    ( NN X. { Y } ) ( ~~>t ` J ) Y ) $=
      ( vm vu vn ctop wcel wa cn csn cxp ctlm cfv wbr wf cv cuz wral wrex cnei
      w3a wss fconstg snssi fss syl11anc adantl simpr c1 wceq fveq2 raleqdv
      rcla4ev 1nn fvconst2g adantll adantlr ssnei snssg biimpar syldan adantr
      eqeltrd elnnuz sylan2br r19.21aiva sylancr 3jca tlmbr mpbird ) AHIZCBIZJZ
      KCLZMZCANOPKBVQQZVNERZVQOZFRZIZEGRZSOZTZGKUAZFVPAUBOOZTZUCVOVRVNWHVNVRVMV
      NKVPVQQVPBUDVRKCBUECBUFKVPBVQUGUHUIVMVNUJVOWFFWGUKKIWBEUKSOZTZWFVOWAWGIZJ
      ZWEWJGUKKWCUKULWBEWDWIWCUKSUMUNUOUPWLWBEWIWLVSKIZWBVSWIIWLWMJVTCWAVOWMVTC
      ULZWKVNWMWNVMKCVSBUQURUSWLCWAIZWMVOWKVPWAUDZWOVMWKWPVNVPAWAUTUSVNWPWOVMVN
      WOWPCWABVAVBURVCVDVEVSVFVGVHVIVHVJFBEGVQABCDVKVL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M m n r s u $.  $d J m n r s u $.  $d X m n r s u $.  $d F m n r s u $.
    $d Y m n r s u $.  $d A m n r s u $.
    lmtlm.1 $e |- J = ( MetOpen ` M ) $.
    lmtlm.2 $e |- X = dom dom M $.
    $( Metric space limits are topological limits under the standard
       topology. $)
    lmtlm $p |- ( ( M e. Met /\ Y e. A /\ F : NN --> X ) ->
                              ( F ( ~~>t ` J ) Y <-> F ( ~~>m ` M ) Y ) ) $=
      ( vm vu vn vr vs cme wcel cn wf w3a cuni cv cfv cuz wral wrex csn cnei
      wa cc0 clt wbr cle co wi cr ctlm clm wceq uniopn2 3ad2ant1 eleq2d anbi1d
      cbl blnei ex 3ad2antl1 imp cz wb eluz nnz syl2an biimprd expimpd adantl
      imim1d simpl1 simprl ffvelrn 3ad2antl3 adantrl elbl3 biimpd syl111anc
      anassrs an32s ad2ant2r a2d syld exp4a ralimdv2 reximdva eleq2 rexralbidv
      rcla4va syl5 mpand expr com23 r19.21adva wss neibl pm2.27 ad2antrr c1
      elnnuz uzss sylbi nnuz syl6ssr sseld eluzle a1i jcad ssel2 adantlr 3expa
      biimpar syldan sylan2 ancoms anasss ancom2s sylan9r impexp syl5ibr
      expcom ancom1s com13 breq2 imbi2d imbi12d rcla4v imdistani exp3a com34
      r19.23adv sylbid r19.21adv impbid pm5.32da bitrd ctop opntop simp2
      eqcomd feq3 syl biimpa 3adant2 eqid tlmbrf 1z lmbrf 3bitr4d ) DNOZFAOZPEB
      QZRZFCSZOZITZBUAZJTZOZIKTZUBUAZUCZKPUDZJFUECUFUAUAZUCZUGZFEOZUHLTZUIUJZUV
      EUVAUKUJZUVBFDULZUVMUIUJZUMZIPUCZKPUDZUMZLUNUCZUGZBFCUOUAUJZBFDUPUAUJUURU
      VKUVLUVJUGUWCUURUUTUVLUVJUURUUSEFUUOUUPUUSEUQUUQDCEHGURZUSUTVAUURUVLUVJUW
      BUURUVLUGZUVJUWBUWFUVJUWALUNUWFUVMUNOZUGUVNUVJUVTUWFUWGUVNUVJUVTUMUWFUWGU
      VNUGZUGZFUVMDVBUAZULZUVIOZUVJUVTUWFUWHUWLUUOUUPUVLUWHUWLUMUUQUUOUVLUGZUWH
      UWLDFUVMCEHGVCVDVEVFUWIUVBUWKOZIUVFUCZKPUDZUVTUWLUVJUGUWIUWOUVSKPUWIUVEPO
      ZUGZUWNUVRIUVFPUWRUVAUVFOZUWNUMZUVAPOZUVOUVQUWRUWTUXAUVOUGZUWNUMUXBUVQUMU
      WRUXBUWSUWNUWQUXBUWSUMUWIUWQUXAUVOUWSUWQUXAUGUWSUVOUVEVGOUVAVGOUWSUVOVHUW
      QUXAUVEUVAVIUVEVJUVAVJVKVLVMVNVOUWRUXBUWNUVQUWRUXBUWNUVQUMZUWIUXAUXCUWQUV
      OUWFUXAUWHUXCUWFUXAUGUWHUXCUURUVLUXAUWHUXCUMZUURUVLUXAUGZUGUUOUVLUVBEOZUX
      DUUOUUPUUQUXEVPUURUVLUXAVQUURUXAUXFUVLUUQUUOUXAUXFUUPPEUVABVRVSZVTUUOUVLU
      XFRZUWHUXCUXHUWHUGUWNUVQUVBDFUVMEHWAWBVDWCWDVFWEWFVDWGWHWIWJWKUVHUWPJUWKU
      VIUVCUWKUQUVDUWNKIPUVFUVCUWKUVBWLWMWNWOWPWQWRWSUWFUWBUVHJUVIUWFUVCUVIOZUW
      BUVHUWFUXIUVCEWTZUHMTZUIUJZFUXKUWJULZUVCWTZUGZMUNUDZUGZUWBUVHUMZUUOUUPUVL
      UXIUXQVHUUQDFCUVCEMHGXAVEUWFUXJUXPUXRUWFUXJUGZUXOUXRMUNUXSUXKUNOZUWBUXOUV
      HUXSUXTUWBUXOUVHUMZUXSUXTUXLUVOUVPUXKUIUJZUMZIPUCZKPUDZUMZUGZUYAUXTUWBUGU
      XOUYGUXSUVHUXOUXTUYFUXSUVHUMZUXOUXTUGUYFUYEUYHUXLUYFUYEUMUXNUXTUXLUYEXBXC
      UXNUXLUXTUYEUYHUMZUXNUXLUXTUYIUXNUXTUXLUYIUXNUXTUXLUGZUGZUXSUYEUVHUXSUYKU
      YEUVHUMUXSUYKUGZUYDUVGKPUYLUWQUGZUYCUVDIPUVFUYMUXBUYBUMZUWSUVDUMZUXAUYCUM
      UYMUYNUWSUYBUMUYOUYMUWSUXBUYBUWQUWSUXBUMUYLUWQUWSUXAUVOUWQUVFPUVAUWQUVFXD
      UBUAZPUWQUVEUYPOUVFUYPWTUVEXEXDUVEXFXGXHXIXJZUWSUVOUMUWQUVEUVAXKXLXMVNVOU
      YMUWSUYBUVDUWQUWSUXAUYLUYBUVDUMZUYQUWFUYKUXAUYRUMZUXJUWFUYJUXNUYSUWFUYJUX
      NUYSUWFUYJUGZUXNUGUXAUYRUYTUXAUXNUYRUYTUXAUGZUXNUGUYBUVDVUAUYBUXNUVDUXNVU
      AUYBUGZUVDUXNUVBUXMOZUVDVUBUXMUVCUVBXNUYTUYBUXAVUCUYTUYBUGUXAUXFVUCUYTUXA
      UXFUYBUWFUXAUXFUYJUURUXAUXFUVLUXGXOXOXOUYTUXFUYBVUCUYTUXFUGVUCUYBUWFUXFUY
      JVUCUYBVHZUWFUXFUYJVUDUWFUXFUYJUGZVUDUUOUUPUVLVUEVUDUMUUQUWMUXFUYJVUDUUOU
      VLUXFUYJVUDUMUXHUYJVUDUVBDFUXKEHWAVDXPVMVEVFWDWEXQWEXRWEXSXTWEVDWEVDYAYBX
      OYCWGWHUXAUVOUYBYDYEWJWKYFWRYBWDYGWHVMYHUXTUWBUYFUWAUYFLUXKUNUVMUXKUQZUVN
      UXLUVTUYEUVMUXKUHUIYIVUFUVRUYCKIPPVUFUVQUYBUVOUVMUXKUVPUIYIYJWMYKYLYMWOYN
      YOYPVMYQWRYRYSYTUUAUURCUUBOZUUPPUUSBQZUWDUVKVHUUOUUPVUGUUQDCGUUCUSUUOUUPU
      UQUUDUUOUUQVUHUUPUUOUUQVUHUUOEUUSUQUUQVUHVHUUOUUSEUWEUUEEUUSPBUUFUUGUUHUU
      IJAIKBCUUSFUUSUUJUUKWCLADFKIBXDEPHUULXHUUMUUN $.
      $( [2-Sep-2009] $)
  $}

