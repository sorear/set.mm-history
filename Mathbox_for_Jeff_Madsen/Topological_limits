$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Topological limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ~~>t $.
  $( Extend class notation with the topological convergence relation. $)
  ctlm $a class ~~>t $.

  ${
    $d j r f x u m n $.
    $( Define the convergence relation on sequences in a topological space.  A
       sequence ` f ` converges to a point ` x ` if for every open neighborhood
       ` U ` of ` x ` , for all large enough ` m ` we have
       ` f ( m ) e. U ` . $)
    df-tlm $a |- ~~>t = { <. j , r >. | ( j e. Top /\ r = { <. f , x >. |
            ( f : NN --> U. j /\ x e. U. j /\ A. u e. ( ( nei ` j ) ` { x } )
                      E. n e. NN A. m e. ( ZZ>= ` n ) ( f ` m ) e. u ) } ) } $.
  $}

  ${
    $d J j r f x u m n $.  $d X j r f x u m n $.  $d F f x u m n $.
    $d Y f x u m n $.  $d A f x u m n $.
    tlmval.1 $e |- X = U. J $.
    $( The convergence relation on sequences in a topological space.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    tlmval $p |- ( J e. Top -> ( ~~>t ` J ) = { <. f , x >. |
                ( f : NN --> X /\ x e. X /\ A. u e. ( ( nei ` J ) ` { x } )
                      E. n e. NN A. m e. ( ZZ>= ` n ) ( f ` m ) e. u ) } ) $=
      ( vj ctop wcel cn cv cfv wral cnei copab cvv wceq wa vr cuz wrex csn ctlm
      wf w3a cxp cpw wss fssxp elpw sylibr anim1i 3impa ssopab2i opabssxp sstri
      vex nnex cuni uniexg syl5eqel xpexg sylancr pwexg syl syl2anc ssexg unieq
      wb syl6eqr eleq2d fveq2 fveq1d raleqdv 3anbi123d opabbidv df-tlm fvopab4g
      feq3 mpdan ) FJKZLGCMZUFZAMZGKZDMWDNBMKDEMUBNOELUCZBWFUDZFPNZNZOZUGZCAQZR
      KZFUENWNSWCWNLGUHZUIZGUHZUJWRRKZWOWNWDWQKZWGTZWLTZCAQWRWMXBCAWEWGWLXBWEWG
      TXAWLWEWTWGWEWDWPUJWTLGWDUKWDWPCUSULUMUNUNUOUPWLCAWQGUQURWCWQRKZGRKZWSWCW
      PRKZXCWCLRKXDXEUTWCGFVAZRHFJVBVCZLGRRVDVEWPRVFVGXGWQGRRVDVHWNWRRVIVEIUAFL
      IMZVAZWDUFZWFXIKZWHBWIXHPNZNZOZUGZCAQWNJRUEXHFSZXOWMCAXPXJWEXKWGXNWLXPXIG
      SXJWEVKXPXIXFGXHFVJHVLZXIGLWDWAVGXPXIGWFXQVMXPWHBXMWKXPWIXLWJXHFPVNVOVPVQ
      VRABCIDEUAVSVTWB $.
      $( [2-Sep-2009] $)

    $( The binary relation " ` F ` converges to ` Y ` topologically".
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    tlmbr $p |- ( ( J e. Top /\ Y e. A ) -> ( F ( ~~>t ` J ) Y <->
          ( F : NN --> X /\ Y e. X /\ A. u e. ( ( nei ` J ) ` { Y } )
                    E. n e. NN A. m e. ( ZZ>= ` n ) ( F ` m ) e. u ) ) ) $=
      ( vf vx wcel cfv wbr cn cv wf wral w3a cvv ctop ctlm cuz wrex cnei tlmval
      copab breqd wceq feq1 fveq1 eleq1d rexralbidv ralbidv 3anbi13d eleq1 sneq
      csn fveq2d raleqdv 3anbi23d eqid nnex fex mpan2 3ad2ant1 brabg2 sylan9bb
      ) FUALZEHFUBMZNEHOGJPZQZKPZGLZCPZVKMZAPZLZCDPUCMZRDOUDZAVMURZFUEMZMZRZSZJ
      KUGZNHBLOGEQZHGLZVOEMZVQLZCVSRDOUDZAHURZWBMZRZSZVIVJWFEHKAJCDFGIUFUHWEWGV
      NWKAWCRZSWOJKEHTBWFVKEUIZVLWGWDWPVNOGVKEUJWQVTWKAWCWQVRWJDCOVSWQVPWIVQVOV
      KEUKULUMUNUOVMHUIZVNWHWPWNWGVMHGUPWRWKAWCWMWRWAWLWBVMHUQUSUTVAWFVBWGWHETL
      ZWNWGOTLWSVCOGTEVDVEVFVGVH $.
      $( [2-Sep-2009] $)

    $( The binary relation " ` F ` converges to ` Y ` topologically".
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    tlmbrf $p |- ( ( J e. Top /\ Y e. A /\ F : NN --> X ) -> ( F ( ~~>t ` J ) Y
                    <-> ( Y e. X /\ A. u e. ( ( nei ` J ) ` { Y } ) E. n e. NN
                                A. m e. ( ZZ>= ` n ) ( F ` m ) e. u ) ) ) $=
      ( ctop wcel cn wf w3a ctlm cfv wbr cv wral wa cuz wrex cnei tlmbr 3adant3
      csn wb 3anass biidd 3anibar syl5bb bitrd ) FJKZHBKZLGEMZNZEHFOPQZUOHGKZCR
      EPARKCDRUAPSDLUBAHUFFUCPPSZNZURUSTZUMUNUQUTUGUOABCDEFGHIUDUEUTUOVATZUPVAU
      OURUSUHUMUNUOVBVAUPVBUIUJUKUL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F a b c d p q u v x y $.  $d J a b c d p q u v x y $.
    $d X a b c d p q u v x y $.
    haustlmu.1 $e |- X = U. J $.
    $( Limits are unique in a Hausdorff space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    haustlmu $p |- ( J e. Haus -> E* x F ( ~~>t ` J ) x ) $=
      ( vy vb va vd vc wcel cv cfv wa wceq wi cn wral wrex syl vu vv vp vq ctlm
      cha wbr wal wmo wf cuz csn cnei w3a ctop wb haustop cvv vex tlmbr anbi12d
      mpan2 3simpc an4 wne cin hausnei opnneip 3expib anim12d adantr rexralbidv
      wn c0 eleq2 rcla4va reeanv caddc co cn0 cz nnz nnnn0 uzaddcl syl2an fveq2
      uzid eleq1d rcla4v cc nncn addcom ancoms eqeltrd inelcm syl6 rexlimivv ex
      sylbir an4s necon2bd syl5bi expdimp exp3a 3impd rexlimdvv mpd 3exp2 imp32
      necon4ad expimpd syl2ani sylbid alrimivv breq2 mo4 sylibr ) CUFKZBALZCUEM
      ZUGZBFLZXTUGZNZXSYBOZPZFUHAUHYAAUIXRYFAFXRYDQDBUJZXSDKZGLZBMZUALZKZGHLZUK
      MZRHQSZUAXSULCUMMZMZRZUNZYGYBDKZILZBMZUBLZKZIJLZUKMZRJQSZUBYBULYPMZRZUNZN
      ZYEXRCUOKZYDUUKUPCUQZUULYAYSYCUUJUULXSURKYAYSUPAUSUAURGHBCDXSEUTVBUULYBUR
      KYCUUJUPFUSUBURIJBCDYBEUTVBVATYSXRYHYRNZYTUUINZYEUUJYGYHYRVCYGYTUUIVCUUNU
      UONYHYTNZYRUUINZNXRYEYHYRYTUUIVDXRUUPUUQYEXRUUPNUUQXSYBXRYHYTXSYBVEZUUQVM
      ZPXRYHYTUURUUSXRYHYTUURUNZNZXSUCLZKZYBUDLZKZUVBUVDVFZVNOZUNZUDCSUCCSUUSXS
      YBUDUCCDEVGUVAUVHUUSUCUDCCUVAUVBCKZUVDCKZNZUVHUUSPUVAUVKNZUVCUVEUVGUUSUVL
      UVCUVEUVGUUSPZUVAUVKUVCUVENZUVMUVKUVNNUVIUVCNZUVJUVENZNZUVAUVMUVIUVJUVCUV
      EVDUVAUVQUVBYQKZUVDUUHKZNZUVMXRUVQUVTPZUUTXRUULUWAUUMUULUVOUVRUVPUVSUULUV
      IUVCUVRXSCUVBVHVIUULUVJUVEUVSYBCUVDVHVIVJTVKUVTUUQUVFVNUVTUUQUVFVNVEZUVRY
      RUVSUUIUWBUVRYRNYJUVBKZGYNRZHQSZUUBUVDKZIUUFRZJQSZUWBUVSUUINYOUWEUAUVBYQY
      KUVBOYLUWCHGQYNYKUVBYJVOVLVPUUGUWHUBUVDUUHUUCUVDOUUDUWFJIQUUFUUCUVDUUBVOV
      LVPUWEUWHNUWDUWGNZJQSHQSUWBUWDUWGHJQQVQUWIUWBHJQQYMQKZUUEQKZNZUWIYMUUEVRV
      SZBMZUVBKZUWNUVDKZNUWBUWLUWDUWOUWGUWPUWLUWMYNKZUWDUWOPUWJYMYNKZUUEVTKUWQU
      WKUWJYMWAKUWRYMWBYMWGTUUEWCUUEYMYMWDWEUWCUWOGUWMYNYIUWMOYJUWNUVBYIUWMBWFW
      HWITUWLUWMUUFKUWGUWPPUWLUWMUUEYMVRVSZUUFUWJYMWJKUUEWJKUWMUWSOUWKYMWKUUEWK
      YMUUEWLWEUWKUWJUWSUUFKZUWKUUEUUFKZYMVTKUWTUWJUWKUUEWAKUXAUUEWBUUEWGTYMWCY
      MUUEUUEWDWEWMWNUWFUWPIUWMUUFUUAUWMOUUBUWNUVDUUAUWMBWFWHWITVJUWNUVBUVDWOWP
      WQWSWEWTWRXAWPXBXCXDXEWRXFXGXHXIXJXKXBXLXMXNYAYCAFXSYBBXTXOXPXQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J m n u $.  $d X m n u $.  $d Y m n u $.
    tlmconst.1 $e |- X = U. J $.
    $( A constant sequence converges to the constant.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    tlmconst $p |- ( ( J e. Top /\ Y e. X ) ->
                                    ( NN X. { Y } ) ( ~~>t ` J ) Y ) $=
      ( vm vu vn wcel wa cn cfv wf cv cuz wral wss c1 wceq adantll adantlr ctop
      csn cxp ctlm wbr wrex cnei w3a fconstg snssi fss syl2anc adantl simpr 1nn
      elnnuz fvconst2g ssnei snssg biimpar syldan adantr eqeltrd sylan2br fveq2
      ralrimiva raleqdv rcla4ev sylancr 3jca tlmbr mpbird ) AUAHZCBHZIZJCUBZUCZ
      CAUDKUEJBVQLZVNEMZVQKZFMZHZEGMZNKZOZGJUFZFVPAUGKKZOZUHVOVRVNWHVNVRVMVNJVP
      VQLVPBPVRJCBUICBUJJVPBVQUKULUMVMVNUNVOWFFWGVOWAWGHZIZQJHWBEQNKZOZWFUOWJWB
      EWKVSWKHWJVSJHZWBVSUPWJWMIVTCWAVOWMVTCRZWIVNWMWNVMJCVSBUQSTWJCWAHZWMVOWIV
      PWAPZWOVMWIWPVNVPAWAURTVNWPWOVMVNWOWPCWABUSUTSVAVBVCVDVFWEWLGQJWCQRWBEWDW
      KWCQNVEVGVHVIVFVJFBEGVQABCDVKVL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M m n r s u $.  $d J m n r s u $.  $d X m n r s u $.  $d F m n r s u $.
    $d Y m n r s u $.  $d A m n r s u $.
    lmtlm.1 $e |- J = ( MetOpen ` M ) $.
    lmtlm.2 $e |- X = dom dom M $.
    $( Metric space limits are topological limits under the standard topology.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    lmtlm $p |- ( ( M e. Met /\ Y e. A /\ F : NN --> X ) ->
                              ( F ( ~~>t ` J ) Y <-> F ( ~~>m ` M ) Y ) ) $=
      ( vm vn vr wcel cn cfv wral wa wbr wi crp wb vu vs cme wf w3a cuni cv cuz
      wrex csn cnei cc0 clt cle co ctlm clm wceq uniopn2 3ad2ant1 eleq2d anbi1d
      cr cbl blnei 3expia 3ad2antl1 imp eleq2 rexralbidv rcla4va cz eluz syl2an
      biimprd expimpd adantl imim1d simpll3 simpl ffvelrn ex simp1 sylanl1 expr
      elbl3 an32s syld adantr pm5.74d sylibd exp4a ralimdv2 reximdva syl5 mpand
      nnz ralrimdva wss neibl breq2 imbi2d rcla4v imdistani impexp elnnuz sylbi
      uzss nnuz syl6sseqr sseld eluzle a1i jcad 3ad2antl3 adantlr simpll1 simpr
      simpllr simplr syl22anc biimpar syldan ssel2 sylan2 ancoms anasss ancom2s
      c1 sylan9r a2d syl5bir com23 exp3a com34 rexlimdv sylbid ralrimdv impbid
      ralrp syl6bb pm5.32da bitr3d ctop opntop simp2 feq3 biimpa 3adant2 tlmbrf
      syl eqid syl3anc 1z lmbrf 3bitr4d ) DUCLZFALZMEBUDZUEZFCUFZLZIUGZBNZUAUGZ
      LZIJUGZUHNZOZJMUIZUAFUJCUKNNZOZPZFELZULKUGZUMQUVGUVCUNQZUVDFDUOZUVOUMQZRZ
      IMOZJMUIZRKVCOZPZBFCUPNQZBFDUQNQUUTUVNUVLPUVMUWCUUTUVNUVBUVLUUTEUVAFUUQUU
      REUVAURZUUSDCEHGUSZUTVAVBUUTUVNUVLUWBUUTUVNPZUVLUWAKSOZUWBUWGUVLUWHUWGUVL
      UWAKSUWGUVOSLZPZFUVODVDNZUOZUVKLZUVLUWAUWGUWIUWMUUQUURUVNUWIUWMRUUSUUQUVN
      UWIUWMDFUVOCEHGVEVFVGVHUWMUVLPUVDUWLLZIUVHOZJMUIZUWJUWAUVJUWPUAUWLUVKUVEU
      WLURUVFUWNJIMUVHUVEUWLUVDVIVJVKUWJUWOUVTJMUWJUVGMLZPZUWNUVSIUVHMUWRUVCUVH
      LZUWNRZUVCMLZUVPUVRUWRUWTUXAUVPPZUWNRUXBUVRRUWRUXBUWSUWNUWQUXBUWSRUWJUWQU
      XAUVPUWSUWQUXAPUWSUVPUWQUVGVLLUVCVLLUWSUVPTUXAUVGWQUVCWQUVGUVCVMVNVOVPVQV
      RUWRUXBUWNUVRUWJUXBUWNUVRTZRUWQUWJUXBUVDELZUXCUWJUXBUXDUWJUUSUXAUXDUXBUUQ
      UURUUSUVNUWIVSUXAUVPVTMEUVCBWAZVNWBUUTUWIUVNUXDUXCRUUTUWIPUVNUXDUXCUUTUUQ
      UWIUVNUXDPUXCUUQUURUUSWCUVDDFUVOEHWFWDWEWGWHWIWJWKWLWMWNWOWPWRUWGUWHUVJUA
      UVKUWGUVEUVKLZUWHUVJUWGUXFUVEEWSZFUBUGZUWKUOZUVEWSZUBSUIZPZUWHUVJRZUUQUUR
      UVNUXFUXLTUUSDFCUVEEUBHGWTVGUWGUXGUXKUXMUWGUXGPZUXJUXMUBSUXNUXHSLZUWHUXJU
      VJUXNUXOUWHUXJUVJRZUXOUWHPUXOUVPUVQUXHUMQZRZIMOZJMUIZPUXNUXPUXOUWHUXTUWAU
      XTKUXHSUVOUXHURZUVSUXRJIMMUYAUVRUXQUVPUVOUXHUVQUMXAXBVJXCXDUXNUXOUXTUXPUX
      NUXOPUXJUXTUVJUXNUXOUXJUXTUVJRZUXNUXJUXOUYBUXNUXJUXOPZPZUXSUVIJMUYDUWQPZU
      XRUVFIMUVHUXAUXRRUXBUXQRZUYEUWSUVFRZUXAUVPUXQXEUYEUYFUWSUXQRUYGUYEUWSUXBU
      XQUWQUWSUXBRUYDUWQUWSUXAUVPUWQUVHMUVCUWQUVHYIUHNZMUWQUVGUYHLUVHUYHWSUVGXF
      YIUVGXHXGXIXJXKZUWSUVPRUWQUVGUVCXLXMXNVQVRUYEUWSUXQUVFUWQUWSUXAUYDUXQUVFR
      ZUYIUWGUYCUXAUYJRZUXGUWGUXOUXJUYKUWGUXOUXJUYKUWGUXOPZUXJPUXAUYJUYLUXAUXJU
      YJUYLUXAPZUXJPUXQUVFUYMUXQUXJUVFUXJUYMUXQPZUVFUYNUXJUVDUXILZUVFUYLUXQUXAU
      YOUYLUXQPUXAUXDUYOUYLUXAUXDUXQUWGUXAUXDUXOUUTUXAUXDUVNUUSUUQUXAUXDUURUXEX
      OXPXPXPUYLUXDUXQUYOUYLUXDPUYOUXQUWGUXDUXOUYOUXQTZUWGUXDPZUXOPUUQUXOUVNUXD
      UYPUYQUUQUXOUUQUURUUSUVNUXDXQWIUYQUXOXRUUTUVNUXDUXOXSUWGUXDUXOXTUVDDFUXHE
      HWFYAWGYBWGYCWGUXIUVEUVDYDYEYFWGWBWGWBYGYHXPYJYKWHYLWMWNYHWEYMVPWOYNYOYPV
      PYQYMYRYSUWAKYTUUAUUBUUCUUTCUUDLZUURMUVABUDZUWDUVMTUUQUURUYRUUSDCGUUEUTUU
      QUURUUSUUFUUQUUSUYSUURUUQUUSUYSUUQUWEUUSUYSTUWFEUVAMBUUGUUKUUHUUIUAAIJBCU
      VAFUVAUULUUJUUMKADFJIBYIEMHUUNXIUUOUUP $.
      $( [15-Jan-2014] $) $( [2-Sep-2009] $)
  $}

