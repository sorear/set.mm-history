$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real and complex numbers; integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.
    $( A finite set of real numbers has a maximum.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    fimaxre $p |- ( ( A C_ RR /\ A e. Fin /\ A =/= (/) )
                                            -> E. x e. A A. y e. A y <_ x ) $=
      ( cr wss cfn wcel c0 wne w3a cv clt wbr wi wral wrex cle wor ltso soss
      mpi fimaxg syl3an1 wa ssel anim12d imp wceq wo wb leloe ancoms eqcom
      orbi2i orcom neor 3bitri syl6bb biimprd syl anassrs ralimdva reximdva
      3ad2ant1 mpd ) CDEZCFGZCHIZJAKZBKZIVJVILMZNZBCOZACPZVJVIQMZBCOZACPZVFCLRZ
      VGVHVNVFDLRVRSCDLTUAABCLUBUCVFVGVNVQNVHVFVMVPACVFVICGZUDVLVOBCVFVSVJCGZVL
      VONZVFVSVTUDZUDVIDGZVJDGZUDZWAVFWBWEVFVSWCVTWDCDVIUECDVJUEUFUGWEVOVLWEVOV
      KVJVIUHZUIZVLWDWCVOWGUJVJVIUKULWGVKVIVJUHZUIWHVKUIVLWFWHVKVJVIUMUNVKWHUOV
      KVIVJUPUQURUSUTVAVBVCVDVE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y $.
    $( A nonempty finite set of real numbers has a maximum.  (Contributed by
       Jeff Madsen, 27-May-2011.) $)
    fimaxre2 $p |- ( ( A C_ RR /\ A e. Fin /\ A =/= (/) )
                                      -> E. x e. RR A. y e. A y <_ x ) $=
      ( cr wss cfn wcel c0 wne w3a cv cle wbr wral wrex fimaxre wi ssrexv
      3ad2ant1 mpd ) CDEZCFGZCHIZJBKAKLMBCNZACOZUDADOZABCPUAUBUEUFQUCUDACDRST
      $.
      $( [27-May-2011] $)
  $}

  ${
    $d A u v w x y z $.  $d B u v w x y z $.  $d ph u v w y $.
    $( Combine a finite set of lower bounds.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    filbcmb $p |- ( ( A e. Fin /\ A =/= (/) /\ B C_ RR ) ->
                      ( A. x e. A E. y e. B A. z e. B ( y <_ z -> ph )
                    -> E. y e. B A. z e. B ( y <_ z -> A. x e. A ph ) ) ) $=
      ( vw vu vv cfn wcel c0 wne cr wss w3a cv cle wbr wi wral wrex cvv reex
      ssex indexfi 3expia sylan2 3adant2 wa r19.2z rexn0 r19.23aivw syl ex
      3ad2ant2 ad2antrr sstr ancoms fimaxre sylan anasss ancom2s 3ad2antl3
      anassrs syld a1dd 3impd ax-17 hbre1 hbral hban hbra1 hbrex wceq breq1
      imbi1d ralbidv cbvrexv ra4 ssel2 adantlr adantrr ad2ant2r adantr
      rcla4cva adantll simplrr letrd pm2.27 ad2antlr mpid syl5 r19.23adva
      syl5bi ralimdva imp an32s exp32 r19.21ai reximdai ssrexv exp43 3ad2ant3
      mpdd ) EJKZELMZFNOZPZCQZDQZRSZATZDFUAZCFUBBEUAZGQZFOZXNCXPUBZBEUAZXNBEUBC
      XPUAZPZGJUBZXLABEUAZTZDFUAZCFUBZXFXHXOYBTZXGXHXFFUCKZYGFNUDUEXFYHXOYBXNBC
      EFUCGUFUGUHUIXIYAYFGJXIXPJKZUJZYAHQZXJRSZHXPUAZCXPUBZYFYJXQXSXTYNYJXQXSXT
      YNTTYJXQUJZXSYNXTYOXSXPLMZYNXIXSYPTZYIXQXGXFYQXHXGXSYPXGXSUJXRBEUBYPXRBEU
      KXRYPBEXNCXPULUMUNUOUPUQXIYIXQYPYNTZXHXFYIXQUJYRXGXHXQYIYRXHXQYIYRXHXQUJZ
      XPNOZYIYRXQXHYTXPFNURUSYTYIYPYNCHXPUTUGVAVBVCVDVEVFVGUOVHXIYAYNYFTZTZYIXH
      XFUUBXGXHXQXSXTUUAXHXQXSXTUUAYSXSXTUJZUJYNYECXPUBZYFYSXSYNUUDTXTYSXSUJZYM
      YECXPYSXSCYSCVIXRCBEBQEKZCVIXNCXPVJVKVLUUEXJXPKZYMYEUUEUUGYMUJZUJYDDFUUEU
      UHDYSXSDYSDVIXRDBEUUFDVIXNDCXPUUGDVIXMDFVMVNVKVLUUHDVIVLYSUUHXSXKFKZYDTYS
      UUHUJZXSUJUUIXLYCUUJUUIXLUJZXSYCUUJUUKUJZXSYCUULXRABEXRIQZXKRSZATZDFUAZIX
      PUBUULUUFUJZAXNUUPCIXPXJUUMVOZXMUUODFUURXLUUNAXJUUMXKRVPVQVRVSUUQUUPAIXPU
      ULUUMXPKZUUPATUUFUUPUUIUUOTZUULUUSUJZAUUODFVTUVAUUTUUNAUVAUUMXJXKUUJUUSUU
      MNKZUUKYSUUSUVBUUHXHXQUUSUVBXQUUSUJXHUUMFKUVBXPFUUMWAFNUUMWAUHVEWBWBUUJXJ
      NKZUUKUUSYSUUGUVCYMXHXQUUGUVCXQUUGUJXHXJFKUVCXPFXJWAFNXJWAUHVEWCUQUULXKNK
      ZUUSYSUUIUVDUUHXLXHUUIUVDXQFNXKWAWBWDWEUUJUUSUUMXJRSZUUKUUHUUSUVEYSYMUUSU
      VEUUGYLUVEHUUMXPYKUUMXJRVPWFWGWGWBUUJUUIXLUUSWHWIUUKUUTUUOTZUUJUUSUUIUVFX
      LUUIUUOWJWEWKWLWMWBWNWOWPWQWRWSWRWTWSXAWCXQUUDYFTXHUUCYECXPFXBWKVFXCVHXDW
      EXEWNVF $.
      $( [2-Sep-2009] $)
  $}

  $( Two nonnegative numbers are zero iff their sum is zero.  (Moved to ~ add20
     in main set.mm and may be deleted by mathbox owner, JM. --NM
     16-Sep-2013.) $)
  add20OLD $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) )
                        -> ( ( A + B ) = 0 <-> ( A = 0 /\ B = 0 ) ) ) $=
    ( add20 ) ABC $.
    $( [2-Sep-2009] $)

  $( Relation between sums and differences..  (Moved to ~ addsubeq4 in main
     set.mm and may be deleted by mathbox owner, JM. --NM 16-Sep-2013.) $)
  addsubeq4OLD $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
                    ( ( A + B ) = ( C + D ) <-> ( C - A ) = ( B - D ) ) ) $=
    ( addsubeq4 ) ABCDE $.
    $( [17-Jun-2010] $)

  $( Natural number exponentiation of a quotient.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  divexp $p |- ( ( ( A e. CC /\ B e. CC /\ B =/= 0 ) /\ N e. NN0 ) ->
                        ( ( A / B ) ^ N ) = ( ( A ^ N ) / ( B ^ N ) ) ) $=
    ( cc wcel cc0 wne w3a cn0 wa cdiv co cexp c1 cmul wceq divrec adantr
    opreq1d simpl1 reccl 3adant1 simpr mulexp syl111anc exprec 3expa 3adantl1
    opreq2d expcl 3ad2antl1 3ad2antl2 simpl3 wi cn wo elnn0 expeq0 biimpd wn
    ax-1ne0 df-ne mpbi opreq2 exp0 sylan9eqr eqeq1d mtbiri pm2.21d jaodan
    sylan2b necon3d mpd eqcomd eqtrd 3eqtrd ) ADEZBDEZBFGZHZCIEZJZABKLZCMLANBKL
    ZOLZCMLZACMLZWDCMLZOLZWGBCMLZKLZWBWCWECMVTWCWEPWAABQRSWBVQWDDEZWAWFWIPVQVRV
    SWATVTWLWAVRVSWLVQBUAUBRVTWAUCAWDCUDUEWBWIWGNWJKLZOLZWKWBWHWMWGOVRVSWAWHWMP
    ZVQVRVSWAWOBCUFUGUHUIWBWGDEZWJDEZWJFGZWNWKPVQVRWAWPVSACUJUKVRVQWAWQVSBCUJUL
    WBVSWRVQVRVSWAUMWBWJFBFVRVQWAWJFPZBFPZUNZVSWAVRCUOEZCFPZUPXACUQVRXBXAXCVRXB
    JWSWTBCURUSVRXCJZWSWTXDWSNFPZNFGXEUTVANFVBVCXDWJNFXCVRWJBFMLNCFBMVDBVEVFVGV
    HVIVJVKULVLVMWPWQWRHWKWNWGWJQVNUEVOVP $.
    $( [2-Sep-2009] $)

  $( An integer mod ` B ` lies in the first ` B ` nonnegative integers.
     (Contributed by Jeff Madsen, 17-Jun-2010.) $)
  zmodfzcl $p |- ( ( A e. ZZ /\ B e. NN ) ->
                                  ( A mod B ) e. ( 0 ... ( B - 1 ) ) ) $=
    ( cz wcel cn wa cmo co cc0 c1 cmin cfz cle wbr wb cn0 zmodcl nn0z syl 0z
    a1i nnz peano2zm adantl elfz syl111anc nn0ge0 clt cr crp zre nnrp modlt
    syl2an zltlem1 syl11anc mpbid mpbir2and ) ACDZBEDZFZABGHZIBJKHZLHDZIVBMNZVB
    VCMNZVAVBCDZICDZVCCDZVDVEVFFOVAVBPDZVGABQZVBRSZVHVATUAUTVIUSUTBCDZVIBUBZBUC
    SUDVBIVCUEUFVAVJVEVKVBUGSVAVBBUHNZVFUSAUIDBUJDVOUTAUKBULABUMUNVAVGVMVOVFOVL
    UTVMUSVNUDVBBUOUPUQUR $.
    $( [17-Jun-2010] $)

  $( Two ways of expressing the remainder when ` A ` is divided by ` B ` .
     (Contributed by Jeff Madsen, 17-Jun-2010.) $)
  rdr $p |- ( ( A e. RR /\ B e. RR+ ) ->
                      ( ( A - ( A mod B ) ) / B ) = ( |_ ` ( A / B ) ) ) $=
    ( cr wcel crp wa cmo co cmin cdiv cfl cfv cmul modval opreq2d cc wceq recn
    adantr rpcn adantl cz rerpdivcl flcl zcn 3syl mulcl syl11anc nncan eqtrd
    opreq1d cc0 wne rpne0 divcan3 syl111anc ) ACDZBEDZFZAABGHZIHZBJHBABJHZKLZMH
    ZBJHZVCUSVAVDBJUSVAAAVDIHZIHZVDUSUTVFAIABNOUSAPDZVDPDZVGVDQUQVHURARSUSBPDZV
    CPDZVIURVJUQBTUAZUSVBCDVCUBDVKABUCVBUDVCUEUFZBVCUGUHAVDUIUHUJUKUSVKVJBULUMZ
    VEVCQVMVLURVNUQBUNUAVCBUOUPUJ $.
    $( [17-Jun-2010] $)

  $( Membership in a later set of upper integers.  (Moved to ~ eluzadd in main
     set.mm and may be deleted by mathbox owner, JM. --NM 2-Sep-2013.) $)
  eluzaddOLD $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. ZZ ) ->
                                      ( N + K ) e. ( ZZ>= ` ( M + K ) ) ) $=
    ( eluzadd ) ABCD $.
    $( [2-Sep-2009] $)

  $( Membership in an earlier set of upper integers.  (Moved to ~ eluzsub in
     main set.mm and may be deleted by mathbox owner, JM. --NM 2-Sep-2013.) $)
  eluzsubOLD $p |- ( ( M e. ZZ /\ K e. ZZ /\ N e. ( ZZ>= ` ( M + K ) ) )
                                          -> ( N - K ) e. ( ZZ>= ` M ) ) $=
    ( eluzsub ) ABCD $.
    $( [2-Sep-2009] $)

  $( Choices for an element of an upper interval of integers.  (Moved to ~ uzm1
     in main set.mm and may be deleted by mathbox owner, JM. --NM
     16-May-2013.) $)
  uzm1OLD $p |- ( N e. ( ZZ>= ` M ) -> ( N = M \/ ( N - 1 ) e. ( ZZ>= ` M ) ) )
      $=
    ( uzm1 ) ABC $.
    $( [2-Sep-2009] $)

  $( Choices for an element of an upper interval of integers.  (Moved to ~ uzp1
     in main set.mm and may be deleted by mathbox owner, JM. --NM
     16-May-2013.) $)
  uzp1OLD $p |- ( N e. ( ZZ>= ` M ) -> ( N = M \/ N e. ( ZZ>= ` ( M + 1 ) ) ) )
      $=
    ( uzp1 ) ABC $.
    $( [2-Sep-2009] $)

  ${
    $d M m n $.  $d N m n $.
    $( A finite interval of integers is finite.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    fzfi $p |- ( N e. ( ZZ>= ` M ) -> ( M ... N ) e. Fin ) $=
      ( vm vn cv cfz co cfn wcel c1 caddc wceq opreq2 eleq1d cz csn wi elfz1eq
      elsn sylibr a1i ssrdv elfz3 snssd eqssd snfi syl6eqel cuz cfv wa cun
      unfi cle wbr eluzel2 eluzelz cr zre syl eluzelre peano2re 3syl eluzle
      lep1 letrd fzsuc syl111anc syl5ibr mpan2i uzind4 ) ACEZFGZHIAAFGZHIADEZFG
      ZHIZAVNJKGZFGZHIZABFGZHICDABVKALZVLVMHVKAAFMNVKVNLVLVOHVKVNAFMNVKVQLVLVRH
      VKVQAFMNVKBLVLVTHVKBAFMNAOIZVMAPZHWBVMWCWBCVMWCVKVMIZVKWCIZQWBWDWAWEVKARC
      ASTUAUBWBAVMAUCUDUEAUFUGVNAUHUIIZVPVQPZHIZVSVQUFVPWHUJVSWFVOWGUKZHIVOWGUL
      WFVRWIHWFWBVNOIZAVQUMUNVRWILAVNUOZAVNUPZWFAVNVQWFWBAUQIWKAURUSAVNUTWFWJVN
      UQIZVQUQIWLVNURZVNVAVBAVNVCWFWJWMVNVQUMUNWLWNVNVDVBVEAVNVFVGNVHVIVJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x $.  $d N x $.  $d A x $.
    $( Variant of ~ fzfi with hypothesis weakened.  (Contributed by Jeff
       Madsen, 16-Jun-2010.) $)
    fzfi2 $p |- ( N e. A -> ( M ... N ) e. Fin ) $=
      ( vx wcel cfz co c0 wceq cfn 0fin eleq1 mpbiri adantl wn cv wex neq0 wa
      cuz cfv elfzuz2 fzfi syl ex 19.23adv imp sylan2b pm2.61dan ) CAEZBCFGZHIZ
      UKJEZULUMUJULUMHJEKUKHJLMNULOUJDPZUKEZDQZUMDUKRUJUPUMUJUOUMDUJUOUMUJUOSCB
      TUAEUMAUNBCUBBCUCUDUEUFUGUHUI $.
      $( [16-Jun-2010] $)
  $}

  $( There are no integers between 1 and 0.  (Moved to ~ fz10 in main set.mm
     and may be deleted by mathbox owner, JM. --NM 16-Sep-2013.) $)
  fz10OLD $p |- ( 1 ... 0 ) = (/) $=
    ( fz10 ) A $.
    $( [16-Jun-2010] $)

  ${
    $d M x $.  $d N x $.  $d A x $.
    $( Properties of a finite interval of integers which is non-empty.  (Moved
       to ~ fzn0 in main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Sep-2013.) $)
    fzn0OLD $p |- ( ( N e. A /\ ( M ... N ) =/= (/) ) ->
                                      ( M e. ZZ /\ N e. ZZ /\ M <_ N ) ) $=
      ( fzn0 ) ABCD $.
      $( [17-Jun-2010] $)
  $}

  $( Membership of a product in a finite interval of integers.  (Contributed by
     Jeff Madsen, 17-Jun-2010.) $)
  fzmul $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. NN ) -> ( J e. ( M ... N ) ->
                        ( K x. J ) e. ( ( K x. M ) ... ( K x. N ) ) ) ) $=
    ( cz wcel cn w3a cfz co cle wbr cmul wb elfz1 3adant3 wi wa cr cc0 clt zre
    nnre nngt0 jca lemul2 syl3an 3expa biimpd adantllr ancom1s adantlll
    anim12d nnz zmulcl ex 3anim123d elfz 3coml syl6 syl com12 imp sylibrd
    an32s exp4b 3impd 3impa sylbid ) CEFZDEFZBGFZHACDIJFZAEFZCAKLZADKLZHZBAMJZB
    CMJZBDMJZIJFZVJVKVMVQNVLACDOPVJVKVLVQWAQVJVKRZVLRZVNVOVPWAWCVNVOVPWAWBVNVLV
    OVPRZWAQWBVNRZVLRZWDVSVRKLZVRVTKLZRZWAWFVOWGVPWHVJVNVLVOWGQVKVJVNRVLRVOWGVJ
    VNVLVOWGNZVJCSFVNASFZVLBSFZTBUALZRZWJCUBAUBZVLWLWMBUCBUDUEZCABUFUGUHUIUJVKV
    NVLVPWHQVJVKVNRVLRVPWHVNVKVLVPWHNZVNVKVLWQVNWKVKDSFVLWNWQWODUBWPADBUFUGUHUK
    UIULUMWEVLWAWINZVJVKVNVLWRQVLVJVKVNHZWRVLBEFZWSWRQBUNWTWSVSEFZVTEFZVREFZHWR
    WTVJXAVKXBVNXCWTVJXABCUOUPWTVKXBBDUOUPWTVNXCBAUOUPUQXCXAXBWRVRVSVTURUSUTVAV
    BUHVCVDVEVFVGVHVI $.
    $( [17-Jun-2010] $)

  $( Membership of a sum in a finite interval of integers.  (Contributed by
     Jeff Madsen, 17-Jun-2010.) $)
  fzadd2 $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( O e. ZZ /\ P e. ZZ ) ) ->
                    ( ( J e. ( M ... N ) /\ K e. ( O ... P ) ) ->
                        ( J + K ) e. ( ( M + O ) ... ( N + P ) ) ) ) $=
    ( cz wcel wa cfz co cle wbr w3a caddc elfz1 bi2anan9 an6 wi wb zaddcl elfz
    syl3an 3expb ancoms 3ad2antr1 cr zre anim12i le2add syl2an impr 3adantr3
    adantlr 3adantr2 adantll mpbir2and ex an4s syl5bi sylbid ) DGHZEGHZIZFGHZAG
    HZIZIZBDEJKHZCFAJKHZIBGHZDBLMZBELMZNZCGHZFCLMZCALMZNZIZBCOKZDFOKZEAOKZJKHZV
    DVIVNVGVJVRBDEPCFAPQVSVKVOIZVLVPIZVMVQIZNZVHWCVKVLVMVOVPVQRVBVEVCVFWGWCSVBV
    EIZVCVFIZIZWGWCWJWGIWCWAVTLMZVTWBLMZWJWEWDWCWKWLITZWFWDWJWMWDWHWIWMWDVTGHWH
    WAGHWIWBGHWMBCUADFUAEAUAVTWAWBUBUCUDUEUFWHWGWKWIWHWDWEWKWFWHWDWEWKWHDUGHZFU
    GHZIBUGHZCUGHZIZWEWKSWDVBWNVEWODUHFUHUIVKWPVOWQBUHCUHUIZDFBCUJUKULUMUNWIWGW
    LWHWIWDWFWLWEWIWDWFWLWDWIWFWLSZWDWREUGHZAUGHZIWTWIWSVCXAVFXBEUHAUHUIBCEAUJU
    KUEULUOUPUQURUSUTVA $.
    $( [17-Jun-2010] $)

  ${
    $d M x $.  $d N x $.  $d K x $.
    $( Split a finite interval of integers into two parts.  (Moved to ~ fzsplit
       in main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Sep-2013.) $)
    fzsplitOLD $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> ( M ... N )
                              = ( ( M ... K ) u. ( ( K + 1 ) ... N ) ) ) $=
      ( fzsplit ) ABCDE $.
      $( [17-Jun-2010] $)

    $d J x $.  $d A x $.
    $( Condition for two finite intervals of integers to be disjoint.  (Moved
       to ~ fzdisj in main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Sep-2013.) $)
    fzdisjOLD $p |- ( ( K e. A /\ K < M ) ->
                                ( ( J ... K ) i^i ( M ... N ) ) = (/) ) $=
      ( fzdisj ) ABCDEF $.
      $( [17-Jun-2010] $)
  $}

  $( Add one to an element of a finite set of integers.  (Contributed by Jeff
     Madsen, 6-Jun-2010.) $)
  fzp1elp1 $p |- ( ( N e. A /\ K e. ( M ... N ) ) ->
                                  ( K + 1 ) e. ( M ... ( N + 1 ) ) ) $=
    ( wcel cfz co wa c1 caddc cz cvv wss elfzel1 adantl oprex fzp1ss sylancl
    simpr wb elfzel2g elfzelz 1z a1i fzaddel syl22anc mpbid sseldd ) DAEZBCDFGE
    ZHZCIJGDIJGZFGZCULFGZBIJGZUKCKEZULLEUMUNMUJUPUIBCDNOZDIJPCULLQRUKUJUOUMEZUI
    UJSUKUPDKEBKEZIKEZUJURTUQABCDUAUJUSUIBCDUBOUTUKUCUDBICDUEUFUGUH $.
    $( [6-Jun-2010] $)

  $( Options for membership in a finite interval of integers.  (Contributed by
     Jeff Madsen, 18-Jun-2010.) $)
  elfzp12 $p |- ( N e. ( ZZ>= ` M ) -> ( K e. ( M ... N ) <->
                                ( K = M \/ K e. ( ( M + 1 ) ... N ) ) ) ) $=
    ( cuz cfv wcel cfz co wceq c1 caddc wo cvv wa elisset anim2i cz eluzel2
    syl eleq1 syl5ibrcom imdistani jaodan csn cun eluzfz1 fzsplit mpdan fzsn
    uneq1d eqtrd eleq2d elun syl6bb elsncg orbi1d sylan9bb pm5.21nd ) CBDEZFZAB
    CGHZFZABIZABJKHCGHZFZLZUTAMFZNZVBVGUTAVAOPUTVCVHVEUTVCVGUTVGVCBMFZUTBQFZVIB
    CRZBQOSABMTUAUBVEVGUTAVDOPUCUTVBABUDZFZVELZVGVFUTVBAVLVDUEZFVNUTVAVOAUTVABB
    GHZVDUEZVOUTBVAFVAVQIBCUFUSBBCUGUHUTVPVLVDUTVJVPVLIVKBUISUJUKULAVLVDUMUNVGV
    MVCVEABMUOUPUQUR $.
    $( [18-Jun-2010] $)

  $( Choices for an element of a finite interval of integers.  (Contributed by
     Jeff Madsen, 2-Sep-2009.) $)
  fzm1 $p |- ( N e. ( ZZ>= ` M ) -> ( K e. ( M ... N ) <->
                              ( K e. ( M ... ( N - 1 ) ) \/ K = N ) ) ) $=
    ( cuz cfv wcel cfz co c1 cmin wceq wo wa wn wi cz cle wbr w3a wb eluzel2
    eluzelz elfz1 syl11anc simp1 a1i simp2 clt wne cr zre eluzelre ltlen
    syl2an ancoms necom df-ne bitri anbi2i ancom syl6bb biimpar an4s zltlem1
    sylan2 biimpd ad2ant2r mpd 3adantr2 ex 3jcad 1z zsubcl sylancl biimprd
    syl6d com23 sylbid imp orrd orcomd caddc cvv wss oprex fzssp1 ax-mp cc zcn
    syl ax-1cn npcan opreq2d syl5sseq sseld eluzfz2 eleq1 syl5ibrcom jaod
    impbid ) CBDEFZABCGHZFZABCIJHZGHZFZACKZLZXAXCXHXAXCMZXGXFXIXGXFXAXCXGNZXFOZ
    XAXCAPFZBAQRZACQRZSZXKXABPFZCPFZXCXOTBCUAZBCUBZABCUCUDXAXJXOXFXAXJXOXLXMAXD
    QRZSZXFXAXJXOYAOXAXJMZXOXLXMXTXOXLOYBXLXMXNUEUFXOXMOYBXLXMXNUGUFYBXOXTYBXLX
    NXTXMYBXLXNMMACUHRZXTXAXLXJXNYCXAXLMZYCXJXNMZYDYCXNCAUIZMZYEXLXAYCYGTZXLAUJ
    FCUJFYHXAAUKBCULACUMUNUOYGXNXJMYEYFXJXNYFACUIXJCAUPACUQURUSXNXJUTURVAVBVCXA
    XLYCXTOZXJXNXLXAYIXLXAMYCXTXAXLXQYCXTTXSACVDVEVFUOVGVHVIVJVKVJXAXFYAXAXPXDP
    FZXFYATXRXAXQIPFYJXSVLCIVMVNABXDUCUDVOVPVQVRVSVTWAVJXAXFXCXGXAXEXBAXABXDIWB
    HZGHZXEXBXDWCFXEYLWDCIJWEBXDWCWFWGXAYKCBGXACWHFZIWHFYKCKXAXQYMXSCWIWJWKCIWL
    VNWMWNWOXAXCXGCXBFBCWPACXBWQWRWSWT $.
    $( [2-Sep-2009] $)

  $( A real number is an integer iff its absolute value is an integer.
     (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  absz $p |- ( A e. RR -> ( A e. ZZ <-> ( abs ` A ) e. ZZ ) ) $=
    ( cr wcel cz cabs cfv cn0 nn0abscl nn0z syl wceq cneg wo wi absor eleq1
    biimpd a1i znegcl cc recn negneg eleq1d syl5ib syl9r jaod mpd impbid2 ) ABC
    ZADCZAEFZDCZUJUKGCULAHUKIJUIUKAKZUKALZKZMULUJNZAOUIUMUPUOUMUPNUIUMULUJUKADP
    QRUOULUNDCZUIUJUOULUQUKUNDPQUQUNLZDCUIUJUNSUIURADUIATCURAKAUAAUBJUCUDUEUFUG
    UH $.
    $( [2-Sep-2009] $)

  $( The difference between a real number and its nearest integer is less than
     or equal to one half.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  rddif $p |- ( A e. RR -> ( abs ` ( ( |_ ` ( A + ( 1 / 2 ) ) ) - A ) )
                                                          <_ ( 1 / 2 ) ) $=
    ( cr wcel c1 c2 cdiv co caddc cfl cfv cmin cabs cle wbr clt wa wceq wb cz
    2re 2ne0 rereccli readdcl mpan2 flcl flbi syl11anc adantr reflcl id flle
    cc0 cc recn addid1 syl 0re halfgt0 ltleii leadd2 mp3an12 mpbii eqbrtrrd
    letrd a1i ltsubadd2 mpd3an23 biimpa sylancl ltadd1 mpbid recnd recni
    addass mp3an23 cmul 2timesi 2cn recidi eqtr3i opreq2d eqtrd breqtrd
    mpbir2and opreq1d fveq2d abssuble0 syl111anc wi resubcl mpdan ltle imp
    eqbrtrd 1z zaddcl sylancr ax-1cn addcom 3eqtrd leaddsub2 biimpar leadd1
    fraclt1 1re ltsubadd halflt1 jctir lt2add syl22anc mpd subsub3 eqtr4d
    subge0 mpbird absid lesub2 subaddrii syl6breq pm2.61ltlei ) ABCZADEFGZHGZIJ
    ZAKGZLJZYBMNAAIJZKGZYBYAYHYBONZPZYFYHYBMYJYFYGAKGZLJZYHYJYEYKLYJYDYGAKYJYDY
    GQZYGYCMNZYCYGDHGZONZYAYMYNYPPRZYIYAYCBCZYGSCZYQYAYBBCZYRETUAUBZAYBUCUDZAUE
    ZYCYGUFUGUHYAYNYIYAYGAYCAUIZYAUJZUUBAUKZYAAULHGZAYCMYAAUMCZUUGAQAUNZAUOUPYA
    ULYBMNZUUGYCMNZULYBUQUUAURUSULBCYTYAUUJUUKRUQUUAULYBAUTVAVBVCVDUHYJYCYGYBHG
    ZYBHGZYOOYJAUULONZYCUUMONZYAYIUUNYAYGBCZYTYIUUNRUUDYTYAUUAVEZAYGYBVFVGVHYAU
    UNUUORZYIYAUULBCZYTUURYAUUPYTUUSUUDUUAYGYBUCVIZUUQAUULYBVJVGUHVKYAUUMYOQYIY
    AUUMYGYBYBHGZHGZYOYAYGUMCZUUMUVBQZYAYGUUDVLZUVCYBUMCZUVFUVDYBUUAVMZUVGYGYBY
    BVNZVOUPYAUVADYGHUVADQYAEYBVPGUVADYBUVGVQEVRUAVSVTZVEWAZWBUHWCWDWEWFYAYLYHQ
    ZYIYAUUPYAYGAMNUVKUUDUUEUUFYGAWGWHUHWBYAYIYHYBMNZYAYHBCZYTYIUVLWIYAUUPUVMUU
    DAYGWJWKZUUAYHYBWLVIWMWNYAYBYHMNZPZYFDYHKGZYBMUVPYFUVQLJZUVQUVPYEUVQLUVPYED
    YGHGZAKGZUVQUVPYDUVSAKUVPYDUVSQZUVSYCMNZYCUVSDHGONZYAUWAUWBUWCPRZUVOYAYRUVS
    SCZUWDUUBYADSCYSUWEWOUUCDYGWPWQYCUVSUFUGUHUVPUUMUVSYCMYAUUMUVSQUVOYAUUMUVBY
    OUVSYAUVCUVFUVFUVDUVEUVFYAUVGVEZUWFUVHWHUVJYAUVCDUMCZYOUVSQUVEWRYGDWSVIWTUH
    UVPUULAMNZUUMYCMNZYAUWHUVOYAUUPYTYAUWHUVORUUDUUQUUEYGYBAXAWHXBYAUWHUWIRZUVO
    YAUUSYAYTUWJUUTUUEUUQUULAYBXCWHUHVKVCYAUWCUVOYAAUVSONZYBDONZPZUWCYAUWKUWLYA
    YHDONZUWKAXDZYAUUPDBCZUWNUWKRUUDUWPYAXEVEZAYGDXFVGVKXGXHYAYAYTUVSBCZUWPUWMU
    WCWIUUEUUQYAUWPUUPUWRXEUUDDYGUCWQUWQAYBUVSDXIXJXKUHWDWEYAUVQUVTQZUVOYAUWGUU
    HUVCUWSUWGYAWRVEUUIUVEDAYGXLWHUHXMWFYAUVRUVQQZUVOYAUVQBCZULUVQMNZUWTYAUWPUV
    MUXAXEUVNDYHWJWQYAUXBYHDMNZYAUWNUXCUWOYAUVMUWPUWNUXCWIUVNXEYHDWLVIXKYAUWPUV
    MUXBUXCRXEUVNDYHXNWQXOUVQXPUGUHWBUVPUVQDYBKGZYBMYAUVOUVQUXDMNZYAYTUVMUWPUVO
    UXERUUQUVNUWQYBYHDXQWHVHDYBYBWRUVGUVGUVIXRXSWNUVNUUQXT $.
    $( [2-Sep-2009] $)

  $( Bound on the absolute value of a real number rounded to the nearest
     integer.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  absrdbnd $p |- ( A e. RR -> ( abs ` ( |_ ` ( A + ( 1 / 2 ) ) ) )
                                        <_ ( ( |_ ` ( abs ` A ) ) + 1 ) ) $=
    ( cr wcel c1 c2 cdiv co caddc cfl cfv cabs cle wbr cc0 clt wa cneg cz 2re
    2ne0 rereccli readdcl mpan2 flcl zre 3syl renegcl syl adantr reflcl
    peano2re cc wceq recn addid1 0re 1re lt01 ltleii 2pos divge0i mp2an wb
    leadd2 mp3an12 mpbii eqbrtrrd flwordi mpd3an23 leneg syl11anc mpbid cmin
    peano2zdi flltp1 wi ltle mpdan mpd ceile recnd ax-1cn negdi2 sylancl zcn
    negneg 3brtr3d a1i lesubadd syl111anc letrd imp leadd1 mp3an23 recni
    addid2i syl6breq 0z flbi halflt1 breqtrri mpbir2an absnid syldan fveq2d
    opreq1d 3brtr4d 1z fladdz breqtrd addge0 an4s mpanr12 flge absid id
    pm2.61ltlei ) ABCZADEFGZHGZIJZKJZAKJZIJZDHGZLMANXRANOMZPZYAQZAQZIJZDHGZYBYE
    LYGYHAIJZQZYKXRYHBCZYFXRYABCZYNXRXTBCZYARCZYOXRXSBCZYPESTUAZAXSUBUCZXTUDZYA
    UEZUFZYAUGUHUIXRYMBCZYFXRYLBCZUUDAUJZYLUGUHZUIXRYKBCZYFXRYJBCZUUHXRYIBCZYJR
    CZUUIAUGZYIUDZYJUEUFZYJUKUHUIXRYHYMLMZYFXRYLYALMZUUOXRYPAXTLMUUPYTXRANHGZAX
    TLXRAULCUUQAUMAUNAUOUHXRNXSLMZUUQXTLMZNDLMNEOMUURNDUPUQURUSUTDEUQSVAVBZNBCZ
    YRXRUURUUSVCUPYSNXSAVDVEVFVGAXTVHVIXRUUEYOUUPUUOVCUUFUUCYLYAVJVKVLUIXRYMYKL
    MZYFXRYMDVMGZYJLMZUVBXRYLDHGZQZYJQZQZUVCYJLXRUVGUVELMZUVFUVHLMZXRUVERCAUVEL
    MZUVIXRYLAUDVNXRAUVEOMZUVKAVOXRUVEBCZUVLUVKVPXRUUEUVMUUFYLUKUHZAUVEVQVRVSAU
    VEVTVIXRUVGBCZUVMUVIUVJVCXRUUIUVOUUNYJUGUHUVNUVGUVEVJVKVLXRYLULCDULCUVFUVCU
    MXRYLUUFWAWBYLDWCWDXRYJULCZUVHYJUMXRUUJUUKUVPUULUUMYJWEUFYJWFUHWGXRUUDDBCZU
    UIUVDUVBVCUUGUVQXRUQWHUUNYMDYJWIWJVLUIWKYGYOYANLMYBYHUMYGYPYQYOXRYPYFYTUIZU
    UAUUBUFYGYAXSIJZNLYGYPYRXTXSLMYAUVSLMUVRYRYGYSWHYGXTNXSHGZXSLYGANLMZXTUVTLM
    ZXRYFUWAXRUVAYFUWAVPUPANVQUCWLZXRUWAUWBVCZYFXRUVAYRUWDUPYSANXSWMWNUIVLXSXSY
    SWOWPWQXTXSVHWJUVSNUMZUURXSNDHGZOMZYRNRCZUWEUURUWGPVCYSWRXSNWSVBUUTXSDUWFOW
    TDWBWPXAXBWQYAXCVKYGYDYJDHYGYCYIIXRYFUWAYCYIUMUWCAXCXDXEXFXGXRNALMZPZYAUVEY
    BYELXRYAUVELMUWIXRYAADHGZIJZUVELXRYPUWKBCXTUWKLMZYAUWLLMYTAUKXRXSDLMZUWMXSD
    YSUQWTUSYRUVQXRUWNUWMVCYSUQXSDAVDVEVFXTUWKVHWJXRDRCUWLUVEUMXHADXIUCXJUIUWJY
    ONYALMZYBYAUMUWJYPYQYOXRYPUWIYTUIZUUAUUBUFUWJNXTLMZUWOUWJYRUURUWQYSUUTXRYRU
    WIUURUWQAXSXKXLXMUWJYPUWHUWQUWOVCUWPWRXTNXNWDVLYAXOVKUWJYDYLDHUWJYCAIAXOXEX
    FXGXRXPUVAXRUPWHXQ $.
    $( [2-Sep-2009] $)

  $( ` A mod B ` is zero iff ` A ` is evenly divisible by ` B ` .  (Contributed
     by Jeff Madsen, 2-Sep-2009.) $)
  mod0 $p |- ( ( A e. RR /\ B e. RR+ ) ->
                              ( ( A mod B ) = 0 <-> ( A / B ) e. ZZ ) ) $=
    ( cr wcel crp wa cmo co cc0 wceq cdiv cfl cfv cz cmul cmin modval eqeq1d
    cc wb recn adantr rpre adantl rerpdivcl reflcl syl remulcl syl11anc recnd
    subeq0 eqcom wne rpcnne0 divmul2 syl111anc syl5rbbr 3bitrd flidz bitrd ) AC
    DZBEDZFZABGHZIJZABKHZLMZVFJZVFNDZVCVEABVGOHZPHZIJZAVJJZVHVCVDVKIABQRVCASDZV
    JSDVLVMTVAVNVBAUAUBZVCVJVCBCDZVGCDZVJCDVBVPVABUCUDVCVFCDZVQABUEZVFUFZUGBVGU
    HUIUJAVJUKUIVHVFVGJZVCVMVFVGULVCVNVGSDZBSDBIUMFZWAVMTVOVCVRWBVSVRVGVTUJUGVB
    WCVABUNUDAVGBUOUPUQURVCVRVHVITVSVFUSUGUT $.
    $( [2-Sep-2009] $)

  $( ` A ` is divisible by ` B ` iff its negative is.  (Contributed by Jeff
     Madsen, 2-Sep-2009.) $)
  negmod0 $p |- ( ( A e. RR /\ B e. RR+ ) ->
                    ( ( A mod B ) = 0 <-> ( -u A mod B ) = 0 ) ) $=
    ( cr wcel crp wa cdiv co cz cneg cmo cc0 wceq znegcl cc rerpdivcl recn
    negneg 3syl eleq1d syl5ib impbid2 wne adantr rpcn adantl rpne0 divneg
    syl111anc bitrd mod0 wb renegcl sylan 3bitr4d ) ACDZBEDZFZABGHZIDZAJZBGHZID
    ZABKHLMVABKHLMZURUTUSJZIDZVCURUTVFUSNVFVEJZIDURUTVENURVGUSIURUSCDUSODVGUSMA
    BPUSQUSRSTUAUBURVEVBIURAODZBODZBLUCZVEVBMUPVHUQAQUDUQVIUPBUEUFUQVJUPBUGUFAB
    UHUITUJABUKUPVACDUQVDVCULAUMVABUKUNUO $.
    $( [2-Sep-2009] $)

  $( ` A ` is divisible by ` B ` iff its absolute value is.  (Contributed by
     Jeff Madsen, 2-Sep-2009.) $)
  absmod0 $p |- ( ( A e. RR /\ B e. RR+ ) ->
                    ( ( A mod B ) = 0 <-> ( ( abs ` A ) mod B ) = 0 ) ) $=
    ( cr wcel crp wa cabs cfv wceq cneg wo cmo co cc0 wb absor adantr wi
    opreq1 eqcoms eqeq1d a1i negmod0 bibi2d syl5ibrcom jaod mpd ) ACDZBEDZFZAGH
    ZAIZUKAJZIZKZABLMZNIZUKBLMZNIZOZUHUOUIAPQUJULUTUNULUTRUJULUPURNUPURIAUKAUKB
    LSTUAUBUJUTUNUQUMBLMZNIZOABUCUNUSVBUQUNURVANUKUMBLSUAUDUEUFUG $.
    $( [2-Sep-2009] $)

  $( The predicate "is a nonnegative real".  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  elnnr $p |- ( X e. ( 0 [,) +oo ) <-> ( X e. RR /\ 0 <_ X ) ) $=
    ( cc0 cpnf cico co wcel cxr cle wbr clt w3a cr wa wb 0re rexr ax-mp pnfxr
    elico1 mp2an cmnf mnflt wi mnfxr xrltletr mp3an12 mpani anim1d xrrebnd
    sylibrd 3impib simp2 jca adantr simpr ltpnf 3jca impbii bitri ) ABCDEFZAGFZ
    BAHIZACJIZKZALFZVBMZBGFZCGFUTVDNBLFZVGOBPQZRBCASTVDVFVDVEVBVAVBVCVEVAVBVCMU
    AAJIZVCMVEVAVBVJVCVAUABJIZVBVJVHVKOBUBQUAGFVGVAVKVBMVJUCUDVIUABAUEUFUGUHAUI
    UJUKVAVBVCULUMVFVAVBVCVEVAVBAPUNVEVBUOVEVCVBAUPUNUQURUS $.
    $( [2-Sep-2009] $)

