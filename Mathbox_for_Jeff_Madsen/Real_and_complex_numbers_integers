$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real and complex numbers; integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

${
  $d A x y $.
  $( A finite set of real numbers has a maximum. $)
  fimaxre $p |- ( ( A (_ RR /\ A e. Fin /\ A =/= (/) )
                                          -> E. x e. A A. y e. A y <_ x ) $=
    ( cr wss cfn wcel c0 wne w3a cv clt wbr wi wral wrex cle wor fimaxg ltso
    soss mpi syl3an1 wa ssel anim12d imp wceq wo wb leloe ancoms eqcom orbi2i
    orcom neor 3bitri syl6bb biimprd syl anassrs r19.20dva r19.22dva 3ad2ant1
    mpd ) CDEZCFGZCHIZJAKZBKZIVJVILMZNZBCOZACPZVJVIQMZBCOZACPZCLRZVGVHVNVFABCLS
    VFDLRVRTCDLUAUBUCVFVGVNVQNVHVFVMVPACVFVICGZUDVLVOBCVFVSVJCGZVLVONZVFVSVTUDZ
    UDVIDGZVJDGZUDZWAVFWBWEVFVSWCVTWDCDVIUECDVJUEUFUGWEVOVLWEVOVKVJVIUHZUIZVLWD
    WCVOWGUJVJVIUKULWGVKVIVJUHZUIWHVKUIVLWFWHVKVJVIUMUNVKWHUOVKVIVJUPUQURUSUTVA
    VBVCVDVE $.
    $( [2-Sep-2009] $)
$}

${
  $d A u v w x y z $. $d B u v w x y z $. $d ph u v w y $.
  $( Combine a finite set of lower bounds. $)
  filbcmb $p |- ( ( A e. Fin /\ A =/= (/) /\ B (_ RR ) ->
                    ( A. x e. A E. y e. B A. z e. B ( y <_ z -> ph )
                      -> E. y e. B A. z e. B ( y <_ z -> A. x e. A ph ) ) ) $=
    ( vw vu vv cfn wcel c0 wne cr wss w3a cv cle wbr wi wral wrex cvv indexf
    3expia reex ssex sylan2 3adant2 wa r19.2z rexn0 a1i r19.23aiv syl ex
    3ad2ant2 ad2antrr fimaxre sstr ancoms sylan anasss ancom2s 3ad2antl3
    anassrs syld a1dd 3impd ax-17 hbre1 hbral hban wel hbra1 hbrex ssel2
    adantlr adantrr ad2ant2r adantr breq1 rcla4cva adantll simplrr letrd
    pm2.27 ad2antlr mpid ra4 syl5 r19.23adva weq imbi1d ralbidv cbvrexv syl5ib
    r19.20dva imp an1rs exp32 r19.21ai r19.22d ssrexv exp43 3ad2ant3 mpdd ) EJK
    ZELMZFNOZPZCQZDQZRSZATZDFUAZCFUBBEUAZGQZFOZXPCXRUBZBEUAZXPBEUBCXRUAZPZGJUBZ
    XNABEUAZTZDFUAZCFUBZXHXJXQYDTZXIXHFUCKZYIXJXHYJXQYDXPBCEFUCGUDUEFNUFUGUHUIX
    KYCYHGJXKXRJKZUJZYCHQZXLRSZHXRUAZCXRUBZYHYLXSYAYBYPYLXSYAYBYPTTYLXSUJZYAYPY
    BYQYAXRLMZYPXKYAYRTZYKXSXIXHYSXJXIYAYRXIYAUJXTBEUBYRXTBEUKXTYRBEXTYRTBQEKZX
    PCXRULUMUNUOUPUQURXKYKXSYRYPTZXJXHYKXSUJUUAXIXJXSYKUUAXJXSYKUUAXRNOZYKUUAXJ
    XSUJZUUBYKYRYPCHXRUSUEXSXJUUBXRFNUTVAVBVCVDVEVFVGVHUPVIXKYCYPYHTZTZYKXJXHUU
    EXIXJXSYAYBUUDXJXSYAYBUUDUUCYAYBUJZUJYPYGCXRUBZYHUUCYAYPUUGTYBUUCYAUJZYOYGC
    XRUUCYACUUCCVJXTCBEYTCVJXPCXRVKVLVMUUHCGVNZYOYGUUHUUIYOUJZUJYFDFUUHUUJDUUCY
    ADUUCDVJXTDBEYTDVJXPDCXRUUIDVJXODFVOVPVLVMUUJDVJVMUUCUUJYAXMFKZYFTUUCUUJUJZ
    YAUJUUKXNYEUULUUKXNUJZYAYEUULUUMUJZYAYEUUNXTABEUUNYTUJZIQZXMRSZATZDFUAZIXRU
    BAXTUUOUUSAIXRUUNIGVNZUUSATYTUUNUUTUJZUUKUURTZAUUSUVAUVBUUQAUVAUUPXLXMUULUU
    TUUPNKZUUMUUCUUTUVCUUJXJXSUUTUVCXJUUPFKUVCXSUUTUJFNUUPVQXRFUUPVQUHVFVRVRUUL
    XLNKZUUMUUTUUCUUIUVDYOXJXSUUIUVDXJXLFKUVDXSUUIUJFNXLVQXRFXLVQUHVFVSURUUNXMN
    KZUUTUUCUUKUVEUUJXNXJUUKUVEXSFNXMVQVRVTWAUULUUTUUPXLRSZUUMUUJUUTUVFUUCYOUUT
    UVFUUIYNUVFHUUPXRYMUUPXLRWBWCWDWDVRUULUUKXNUUTWEWFUUMUVBUURTZUULUUTUUKUVGXN
    UUKUURWGWAWHWIUURDFWJWKVRWLXPUUSCIXRCIWMZXOUURDFUVHXNUUQAXLUUPXMRWBWNWOWPWQ
    WRWSWTXAWTXBXAXCVSXSUUGYHTXJUUFYGCXRFXDWHVGXEVIXFWAXGWLVG $.
    $( [2-Sep-2009] $)
$}

${
  $( Two nonnegative numbers are zero iff their sum is zero. $)
  add20 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) )
                        -> ( ( A + B ) = 0 <-> ( A = 0 /\ B = 0 ) ) ) $=
    ( cr wcel cc0 cle wbr caddc co wceq wa wb wi cif breq2 anbi1d opreq1
    eqeq1d eqeq1 bibi12d imbi12d anbi2d opreq2 0re elimel add20i dedth2h imp
    an4s ) ACDZBCDZEAFGZEBFGZABHIZEJZAEJZBEJZKZLZUJUKKULUMKZUSUJUKUTUSMEUJAENZF
    GZUMKZVABHIZEJZVAEJZUQKZLZMVBEUKBENZFGZKZVAVIHIZEJZVFVIEJZKZLZMABEEAVAJZUTV
    CUSVHVQULVBUMAVAEFOPVQUOVEURVGVQUNVDEAVABHQRVQUPVFUQAVAESPTUABVIJZVCVKVHVPV
    RUMVJVBBVIEFOUBVRVEVMVGVOVRVDVLEBVIVAHUCRVRUQVNVFBVIESUBTUAVAVIAECUDUEBECUD
    UEUFUGUHUI $.
    $( [2-Sep-2009] $)
$}

${
  $( Natural number exponentiation of a quotient. $)
  divexp $p |- ( ( ( A e. CC /\ B e. CC /\ B =/= 0 ) /\ N e. NN0 ) ->
                        ( ( A / B ) ^ N ) = ( ( A ^ N ) / ( B ^ N ) ) ) $=
    ( cc wcel cc0 wne w3a cn0 wa cdiv co cexp c1 cmul wceq divrec adantr
    opreq1d mulexp 3simp1 reccl 3adant1 pm3.27 syl3anc exprec 3expa 3adantl1
    opreq2d eqcomd expcl 3ad2antl1 3ad2antl2 3simp3 wi cn wo expeq0 biimpd wn
    ax1ne0 df-ne mpbi opreq2 exp0 sylan9eqr eqeq1d mtbiri pm2.21d jaodan elnn0
    sylan2b necon3d mpd eqtrd 3eqtrd ) ADEZBDEZBFGZHZCIEZJZABKLZCMLANBKLZOLZCML
    ZACMLZWDCMLZOLZWGBCMLZKLZWBWCWECMVTWCWEPWAABQRSVQWDDEZWAWFWIPWBAWDCTVTVQWAV
    QVRVSUARVTWLWAVRVSWLVQBUBUCRVTWAUDUEWBWIWGNWJKLZOLZWKWBWHWMWGOVRVSWAWHWMPZV
    QVRVSWAWOBCUFUGUHUIWGDEZWJDEZWJFGZWNWKPWBWPWQWRHWKWNWGWJQUJVQVRWAWPVSACUKUL
    VRVQWAWQVSBCUKUMWBVSWRVTVSWAVQVRVSUNRWBWJFBFVRVQWAWJFPZBFPZUOZVSVRCUPEZCFPZ
    UQXAWAVRXBXAXCVRXBJWSWTBCURUSVRXCJZWSWTXDWSNFPZNFGXEUTVANFVBVCXDWJNFXCVRWJB
    FMLNCFBMVDBVEVFVGVHVIVJCVKVLUMVMVNUEVOVP $.
    $( [2-Sep-2009] $)
$}

${
  $( Membership in a later set of upper integers. $)
  eluzadd $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. ZZ ) ->
                                      ( N + K ) e. ( ZZ>= ` ( M + K ) ) ) $=
    ( cuz cfv wcel cz caddc co eluzel2 wa wi cc0 cif wceq fveq2 eleq2d opreq1
    fveq2d imbi12d opreq2 eleq12d imbi2d 0z elimel eluzaddi dedth2h com12
    mpand imp ) CBDEZFZAGFZCAHIZBAHIZDEZFZULBGFZUMUQBCJURUMKULUQURUMULUQLCURBMN
    ZDEZFZUNUSAHIZDEZFZLVACUMAMNZHIZUSVEHIZDEZFZLBAMMBUSOZULVAUQVDVJUKUTCBUSDPQ
    VJUPVCUNVJUOVBDBUSAHRSQTAVEOZVDVIVAVKUNVFVCVHAVECHUAVKVBVGDAVEUSHUASUBUCVEU
    SCBMGUDUEAMGUDUEUFUGUHUIUJ $.
    $( [2-Sep-2009] $)
$}

${
  $( Membership in an earlier set of upper integers. $)
  eluzsub $p |- ( ( M e. ZZ /\ K e. ZZ /\ N e. ( ZZ>= ` ( M + K ) ) )
                                          -> ( N - K ) e. ( ZZ>= ` M ) ) $=
    ( cz wcel caddc co cuz cfv cmin wi cc0 cif wceq opreq1 fveq2d eleq2d fveq2
    imbi12d opreq2 eleq1d 0z elimel eluzsubi dedth2h 3impia ) BDEZADEZCBAFGZHIZ
    EZCAJGZBHIZEZUGUHUKUNKCUGBLMZAFGZHIZEZULUOHIZEZKCUOUHALMZFGZHIZEZCVAJGZUSEZ
    KBALLBUONZUKURUNUTVGUJUQCVGUIUPHBUOAFOPQVGUMUSULBUOHRQSAVANZURVDUTVFVHUQVCC
    VHUPVBHAVAUOFTPQVHULVEUSAVACJTUASVAUOCBLDUBUCALDUBUCUDUEUF $.
    $( [2-Sep-2009] $)
$}

${
  $( Choices for an element of an upper interval of integers. $)
  uzm1 $p |- ( N e. ( ZZ>= ` M ) -> ( N = M \/ ( N - 1 ) e. ( ZZ>= ` M ) ) ) $=
    ( cuz cfv wcel wceq c1 cmin co wn cz cle wbr w3a eluzel2 a1d eluzelz
    peano2zm syl clt wne eluzle wa cr wb ltlen zre sylanc biimprd mpand df-ne
    syl5ibr zltlem1 sylibd 3jcad eluz2 syl6ibr orrd ) BACDZEZBAFZBGHIZUSEZUTVAJ
    ZAKEZVBKEZAVBLMZNVCUTVDVEVFVGUTVEVDABOZPUTVFVDUTBKEZVFABQZBRSPUTVDABTMZVGUT
    BAUAZVKVDUTABLMZVLVKABUBUTVKVMVLUCZAUDEZBUDEZVKVNUEUTABUFUTVEVOVHAUGSUTVIVP
    VJBUGSUHUIUJBAUKULVEVIVKVGUEUTABUMVHVJUHUNUOAVBUPUQUR $.
    $( [2-Sep-2009] $)
$}

${
  $( Choices for an element of an upper interval of integers. $)
  uzp1 $p |- ( N e. ( ZZ>= ` M ) -> ( N = M \/ N e. ( ZZ>= ` ( M + 1 ) ) ) ) $=
    ( cuz cfv wcel wceq c1 cmin co wo caddc uzm1 cz cc eluzelz zcn ax1cn npcan
    mpan2 3syl eleq1d eluzp1p1 syl5bi orim2d mpd ) BACDZEZBAFZBGHIZUFEZJUHBAGKI
    CDZEZJABLUGUJULUHUGUIGKIZUKEULUJUGUMBUKUGBMEBNEZUMBFZABOBPUNGNEUOQBGRSTUAAU
    IUBUCUDUE $.
    $( [2-Sep-2009] $)
$}

${
  $d M m n $. $d N m n $.
  $( A finite interval of integers is finite. $)
  fzfi $p |- ( N e. ( ZZ>= ` M ) -> ( M ... N ) e. Fin ) $=
    ( vm vn cv cfz co cfn wcel c1 caddc wceq opreq2 eleq1d cz csn wi elfz1eq
    elsn sylibr a1i ssrdv wss elfz3 snssg mpbid eqssd snfi syl6eqel cuz cfv
    cun wa cle wbr fzsuc eluzel2 eluzelz cr zre syl peano2re 3syl eluzle lep1
    letrd syl3anc unfi syl5bir mpan2i uzind4 ) ACEZFGZHIAAFGZHIADEZFGZHIZAVOJKG
    ZFGZHIZABFGZHICDABVLALZVMVNHVLAAFMNVLVOLVMVPHVLVOAFMNVLVRLVMVSHVLVRAFMNVLBL
    VMWAHVLBAFMNAOIZVNAPZHWCVNWDWCCVNWDVLVNIZVLWDIZQWCWEWBWFVLARCASTUAUBWCAVNIW
    DVNUCAUDAVNOUEUFUGAUHUIVOAUJUKIZVQVRPZHIZVTVRUHWGVTVPWHULZHIVQWIUMWGVSWJHWC
    VOOIZAVRUNUOVSWJLWGAVOUPAVOUQZAVOURZWGAVOVRWGWCAUSIWLAUTVAWGWKVOUSIZWMVOUTZ
    VAWGWKWNVRUSIWMWOVOVBVCAVOVDWGWKWNVOVRUNUOWMWOVOVEVCVFVGNVPWHVHVIVJVK $.
    $( [2-Sep-2009] $)
$}

${
  $d M m $.
  $( A finite interval of integers with one element. $)
  fzsn $p |- ( M e. ZZ -> ( M ... M ) = { M } ) $=
    ( vm cz wcel cfz co csn cv wceq elfz1eq eleq1 elfz3 syl5cbir impbid2 elsn
    syl6bbr eqrdv ) ACDZBAAEFZAGZRBHZSDZUAAIZUATDRUBUCUAAJUCUBASDRUAASKALMNBAOP
    Q $.
    $( [2-Sep-2009] $)
$}

${
  $d M m $.
  $( A finite interval of integers with two elements. $)
  fzpr $p |- ( M e. ZZ -> ( M ... ( M + 1 ) ) = { M , ( M + 1 ) } ) $=
    ( vm cz wcel c1 caddc co cfz cpr cv wceq wo cuz cfv wb uzid elfzp1 syl csn
    fzsn eleq2d elsn syl6bb orbi1d bitrd visset elpr syl6bbr eqrdv ) ACDZBAAEFG
    ZHGZAUKIZUJBJZULDZUNAKZUNUKKZLZUNUMDUJUOUNAAHGZDZUQLZURUJAAMNDUOVAOAPUNAAQR
    UJUTUPUQUJUTUNASZDUPUJUSVBUNATUABAUBUCUDUEUNAUKBUFUGUHUI $.
    $( [2-Sep-2009] $)
$}

${
  $( Choices for an element of a finite interval of integers. $)
  fzm1 $p |- ( N e. ( ZZ>= ` M ) -> ( K e. ( M ... N ) <->
                              ( K e. ( M ... ( N - 1 ) ) \/ K = N ) ) ) $=
    ( cuz cfv wcel cfz co c1 cmin wceq wo wa wn wi cz cle wbr w3a wb elfz1
    eluzel2 eluzelz sylanc 3simp1 a1i 3simp2 clt wne cr ltlen zre syl syl2an
    ancoms necom df-ne bitri anbi2i ancom syl6bb biimpar an4s zltlem1 sylan2
    biimpd ad2ant2r mpd 3adantr2 ex 3jcad zsubcl 1z sylancl biimprd syl6d
    com23 sylbid imp orrd orcom sylib caddc wss fzssp1 cc npcan zcn ax1cn
    opreq2d sseqtrd sseld eleq1 eluzfz2 syl5cbir jaod impbid ) CBDEFZABCGHZFZAB
    CIJHZGHZFZACKZLZWRWTXEWRWTMZXDXCLXEXFXDXCWRWTXDNZXCOZWRWTAPFZBAQRZACQRZSZXH
    BPFZCPFZWTXLTWRABCUABCUBZBCUCZUDWRXGXLXCWRXGXLXIXJAXAQRZSZXCWRXGXLXROWRXGMZ
    XLXIXJXQXLXIOXSXIXJXKUEUFXLXJOXSXIXJXKUGUFXSXLXQXSXIXKXQXJXSXIXKMMACUHRZXQW
    RXIXGXKXTWRXIMZXTXGXKMZYAXTXKCAUIZMZYBXIWRXTYDTZAUJFCUJFZYEXIWRACUKAULWRXNY
    FXPCULUMUNUOYDXKXGMYBYCXGXKYCACUIXGCAUPACUQURUSXKXGUTURVAVBVCWRXIXTXQOZXGXK
    XIWRYGXIWRMXTXQXIXNXTXQTWRACVDXPVEVFUOVGVHVIVJVKVJWRXCXRXMXAPFZXCXRTWRABXAU
    AXOXNIPFYHWRCIVLXPVMVNZUDVOVPVQVRVSVTXDXCWAWBVJWRXCWTXDWRXBWSAWRXBBXAIWCHZG
    HZWSXMYHXBYKWDWRBXAWEXOYIUDWRYJCBGCWFFZIWFFYJCKWRCIWGWRXNYLXPCWHUMWIVNWJWKW
    LXDWTCWSFWRACWSWMBCWNWOWPWQ $.
    $( [2-Sep-2009] $)
$}

${
  $( A real number is an integer iff its absolute value is an integer. $)
  absz $p |- ( A e. RR -> ( A e. ZZ <-> ( abs ` A ) e. ZZ ) ) $=
    ( cr wcel cz cabs cfv cn0 nn0abscl nn0z syl wceq cneg wo wi absor eleq1
    biimpd a1i cc recn negneg eleq1d znegcl syl5bi syl9r jaod mpd impbid2 ) ABC
    ZADCZAEFZDCZUJUKGCULAHUKIJUIUKAKZUKALZKZMULUJNZAOUIUMUPUOUMUPNUIUMULUJUKADP
    QRUOULUNDCZUIUJUOULUQUKUNDPQUIUNLZDCUJUQUIURADUIASCURAKATAUAJUBUNUCUDUEUFUG
    UH $.
    $( [2-Sep-2009] $)
$}

${
  $( The difference between a real number and its nearest integer is less
  than or equal to one half. $)
  rddif $p |- ( A e. RR -> ( abs ` ( ( |_ ` ( A + ( 1 / 2 ) ) ) - A ) )
                                                          <_ ( 1 / 2 ) ) $=
    ( cr wcel c1 c2 cdiv co caddc cfl cfv cmin cabs cle wbr clt wa wceq reflcl
    id 2re 2ne0 rereccli readdcl mpan2 flle cc0 cc recn ax0id syl 0re halfgt0
    ltleii wb leadd2 mp3an12 mpbii eqbrtrrd letrd adantr a1i ltsubadd2
    mpd3an23 biimpa sylancl ltadd1 mpbid recnd recni addass mp3an23 cmul
    2timesi 2cn recidi eqtr3i opreq2d eqtrd breqtrd jca cz flbi flcl sylanc
    mpbird opreq1d fveq2d abssuble0 syl3anc wi ltle resubcl mpdan imp eqbrtrd
    addcom ax1cn 3eqtrd leaddsub2 biimpar leadd1 fraclt1 1re ltsubadd halflt1
    jctir lt2add sylancr syl2anc mpd zaddcl 1z subsub3 eqtr4d absid subge0
    lesub2 subaddrii syl6breq pm2.61ltlei ) ABCZADEFGZHGZIJZAKGZLJZYBMNAAIJZKGZ
    YBYAYHYBONZPZYFYHYBMYJYFYGAKGZLJZYHYJYEYKLYJYDYGAKYJYDYGQZYGYCMNZYCYGDHGZON
    ZPZYJYNYPYAYNYIYAYGAYCARZYASZYAYBBCZYCBCZETUAUBZAYBUCUDZAUEZYAAUFHGZAYCMYAA
    UGCZUUEAQAUHZAUIUJYAUFYBMNZUUEYCMNZUFYBUKUUBULUMUFBCYTYAUUHUUIUNUKUUBUFYBAU
    OUPUQURUSUTYJYCYGYBHGZYBHGZYOOYJAUUJONZYCUUKONZYAYIUULYAYGBCZYTYIUULUNYRYTY
    AUUBVAZAYGYBVBVCVDYAUULUUMUNZYIYAUUJBCZYTUUPUUNYTUUQYAYGYBUCYRUUBVEZUUOAUUJ
    YBVFVCUTVGYAUUKYOQYIYAUUKYGYBYBHGZHGZYOYAYGUGCZUUKUUTQZYAYGYRVHZUVAYBUGCZUV
    DUVBYBUUBVIZUVEYGYBYBVJZVKUJYAUUSDYGHUUSDQYAEYBVLGUUSDYBUVEVMEVNUAVOVPZVAVQ
    ZVRUTVSVTYAYMYQUNZYIUUAYGWACZUVIYAYCYGWBUUCAWCZWDUTWEWFWGYAYLYHQZYIUUNYAYGA
    MNUVLYAYGAWHYRYSUUDWIUTVRYAYIYHYBMNZYHBCZYTYIUVMWJYAYHYBWKYAUUNUVNYRAYGWLWM
    ZUUBVEWNWOYAYBYHMNZPZYFDYHKGZYBMUVQYFUVRLJZUVRUVQYEUVRLUVQYEDYGHGZAKGZUVRUV
    QYDUVTAKUVQYDUVTQZUVTYCMNZYCUVTDHGONZPZUVQUWCUWDUVQUUKUVTYCMYAUUKUVTQUVPYAU
    UKUUTYOUVTUVAUVDUVDUVBYAUVFUVCUVDYAUVEVAZUWFWIUVHUVADUGCZYOUVTQYAYGDWPUVCWQ
    VEWRUTUVQUUJAMNZUUKYCMNZYAUWHUVPUUNYTYAUWHUVPUNYAYGYBAWSYRUUOYSWIWTYAUWHUWI
    UNZUVPUUQYAYTUWJYAUUJAYBXAUURYSUUOWIUTVGURYAUWDUVPYAAUVTONZYBDONZPZUWDYAUWK
    UWLYAYHDONZUWKAXBZYAUUNDBCZUWNUWKUNYRUWPYAXCVAZAYGDXDVCVGXEXFYAYTUVTBCZUWPU
    WMUWDWJYAAYBUVTDXGYSUUOUWPUUNUWRYADYGUCXCYRXHUWQXIXJUTVTYAUWBUWEUNZUVPUUAUV
    TWACZUWSYAYCUVTWBUUCDWACUVJUWTYADYGXKXLUVKXHWDUTWEWFYAUVRUWAQZUVPUWGUUFUVAU
    XAYADAYGXMUWGYAWQVAUUGUVCWIUTXNWGYAUVSUVRQZUVPUVRBCZUFUVRMNZUXBYAUVRXOUWPUV
    NUXCYADYHWLXCUVOXHYAUXDYHDMNZYAUWNUXEUWOUVNUWPUWNUXEWJYAYHDWKUVOXCVEXJUWPUV
    NUXDUXEUNYADYHXPXCUVOXHWEWDUTVRUVQUVRDYBKGZYBMYAUVPUVRUXFMNZYTUVNUWPUVPUXGU
    NYAYBYHDXQUUOUVOUWQWIVDDYBYBWQUVEUVEUVGXRXSWOUVOUUOXT $.
    $( [2-Sep-2009] $)
$}

${
  $( Bound on the absolute value of a real number rounded to the nearest
  integer. $)
  absrdbnd $p |- ( A e. RR -> ( abs ` ( |_ ` ( A + ( 1 / 2 ) ) ) )
                                        <_ ( ( |_ ` ( abs ` A ) ) + 1 ) ) $=
    ( cr wcel c1 c2 cdiv co caddc cfl cfv cabs cle wbr cc0 clt wa cneg cz 2re
    2ne0 rereccli readdcl mpan2 flcl zre 3syl renegcl syl adantr reflcl
    peano2re cc wceq recn addid1 0re 1re lt01 ltleii 2pos divge0i mp2an wb
    leadd2 mp3an12 mpbii eqbrtrrd flwordi mpd3an23 leneg sylanc mpbid cmin
    peano2zdi flltp1 wi ltle mpdan mpd ceile negdi2 recnd ax1cn sylancl zcn
    negneg 3brtr3d lesubadd a1i syl3anc letrd absnid imp leadd1 mp3an23 recni
    addid2i syl6breq 0z flbi halflt1 breqtrri mpbir2an syldan fveq2d opreq1d
    3brtr4d 1z fladdz breqtrd absid addge0 an4s mpanr12 flge id pm2.61ltlei )
    ABCZADEFGZHGZIJZKJZAKJZIJZDHGZLMANXRANOMZPZYAQZAQZIJZDHGZYBYELYGYHAIJZQZYKX
    RYHBCZYFXRYABCZYNXRXTBCZYARCZYOXRXSBCZYPESTUAZAXSUBUCZXTUDZYAUEZUFZYAUGUHUI
    XRYMBCZYFXRYLBCZUUDAUJZYLUGUHZUIXRYKBCZYFXRYJBCZUUHXRYIBCZYJRCZUUIAUGZYIUDZ
    YJUEUFZYJUKUHUIXRYHYMLMZYFXRYLYALMZUUOXRYPAXTLMUUPYTXRANHGZAXTLXRAULCUUQAUM
    AUNAUOUHXRNXSLMZUUQXTLMZNDLMNEOMUURNDUPUQURUSUTDEUQSVAVBZNBCZYRXRUURUUSVCUP
    YSNXSAVDVEVFVGAXTVHVIUUEYOUUPUUOVCXRYLYAVJUUFUUCVKVLUIXRYMYKLMZYFXRYMDVMGZY
    JLMZUVBXRYLDHGZQZYJQZQZUVCYJLXRUVGUVELMZUVFUVHLMZXRUVERCAUVELMZUVIXRYLAUDVN
    XRAUVEOMZUVKAVOXRUVEBCZUVLUVKVPXRUUEUVMUUFYLUKUHZAUVEVQVRVSAUVEVTVIUVGBCZUV
    MUVIUVJVCXRUVGUVEVJXRUUIUVOUUNYJUGUHUVNVKVLYLULCDULCUVFUVCUMXRYLDWAXRYLUUFW
    BWCWDXRYJULCZUVHYJUMXRUUJUUKUVPUULUUMYJWEUFYJWFUHWGUUDDBCZUUIUVDUVBVCXRYMDY
    JWHUUGUVQXRUQWIUUNWJVLUIWKYOYANLMYBYHUMYGYAWLYGYPYQYOXRYPYFYTUIZUUAUUBUFYGY
    AXSIJZNLYPYRXTXSLMYAUVSLMYGXTXSVHUVRYRYGYSWIYGXTNXSHGZXSLYGANLMZXTUVTLMZXRY
    FUWAXRUVAYFUWAVPUPANVQUCWMZXRUWAUWBVCZYFXRUVAYRUWDUPYSANXSWNWOUIVLXSXSYSWPW
    QWRWJUVSNUMZUURXSNDHGZOMZYRNRCZUWEUURUWGPVCYSWSXSNWTVBUUTXSDUWFOXADWCWQXBXC
    WRVKYGYDYJDHYGYCYIIXRYFUWAYCYIUMUWCAWLXDXEXFXGXRNALMZPZYAUVEYBYELXRYAUVELMU
    WIXRYAADHGZIJZUVELYPUWKBCXTUWKLMZYAUWLLMXRXTUWKVHYTAUKXRXSDLMZUWMXSDYSUQXAU
    SYRUVQXRUWNUWMVCYSUQXSDAVDVEVFWJXRDRCUWLUVEUMXHADXIUCXJUIYONYALMZYBYAUMUWJY
    AXKUWJYPYQYOXRYPUWIYTUIZUUAUUBUFUWJNXTLMZUWOUWJYRUURUWQYSUUTXRYRUWIUURUWQAX
    SXLXMXNYPUWHUWQUWOVCUWJXTNXOUWPWSWDVLVKUWJYDYLDHUWJYCAIAXKXEXFXGXRXPUVAXRUP
    WIXQ $.
    $( [2-Sep-2009] $)
$}

${
  $( ` A mod B ` is zero iff ` A ` is evenly divisible by ` B ` . $)
  mod0 $p |- ( ( A e. RR /\ B e. RR+ ) ->
                              ( ( A mod B ) = 0 <-> ( A / B ) e. ZZ ) ) $=
    ( cr wcel crp wa cmo co cc0 wceq cdiv cfl cfv cz cmul cmin modval eqeq1d
    cc wb subeq0 recn adantr remulcl rpre adantl rerpdivcl reflcl syl sylanc
    recnd wne divmul2 rpcnne0 syl3anc eqcom syl5rbbr 3bitrd flidz bitrd ) ACDZB
    EDZFZABGHZIJZABKHZLMZVFJZVFNDZVCVEABVGOHZPHZIJZAVJJZVHVCVDVKIABQRASDZVJSDVL
    VMTVCAVJUAVAVNVBAUBUCZVCVJBCDZVGCDZVJCDVCBVGUDVBVPVABUEUFVCVFCDZVQABUGZVFUH
    ZUIUJUKUJVCVFVGJZVMVHVNVGSDZBSDBIULFZWAVMTVCAVGBUMVOVCVRWBVSVRVGVTUKUIVBWCV
    ABUNUFUOVFVGUPUQURVCVRVHVITVSVFUSUIUT $.
    $( [2-Sep-2009] $)
$}

${
  $( ` A ` is divisible by ` B ` iff its negative is. $)
  negmod0 $p |- ( ( A e. RR /\ B e. RR+ ) ->
                    ( ( A mod B ) = 0 <-> ( -u A mod B ) = 0 ) ) $=
    ( cr wcel crp wa cdiv co cz cneg cmo cc0 wceq znegcl cc rerpdivcl recn
    negneg 3syl eleq1d syl5bi impbid2 wne divneg adantr rpcn adantl rpne0
    syl3anc bitrd mod0 wb renegcl sylan 3bitr4d ) ACDZBEDZFZABGHZIDZAJZBGHZIDZA
    BKHLMVABKHLMZURUTUSJZIDZVCURUTVFUSNURVEJZIDUTVFURVGUSIURUSCDUSODVGUSMABPUSQ
    USRSTVENUAUBURVEVBIAODZBODZBLUCZVEVBMURABUDUPVHUQAQUEUQVIUPBUFUGUQVJUPBUHUG
    UITUJABUKVACDUQVDVCULUPVABUKAUMUNUO $.
    $( [2-Sep-2009] $)
$}

${
  $( ` A ` is divisible by ` B ` iff its absolute value is. $)
  absmod0 $p |- ( ( A e. RR /\ B e. RR+ ) ->
                    ( ( A mod B ) = 0 <-> ( ( abs ` A ) mod B ) = 0 ) ) $=
    ( cr wcel crp wa cabs cfv wceq cneg wo cmo co cc0 wb absor adantr wi
    opreq1 eqcoms eqeq1d a1i negmod0 bibi2d biimprcd syl jaod mpd ) ACDZBEDZFZA
    GHZAIZULAJZIZKZABLMZNIZULBLMZNIZOZUIUPUJAPQUKUMVAUOUMVARUKUMUQUSNUQUSIAULAU
    LBLSTUAUBUKURUNBLMZNIZOZUOVARABUCUOVAVDUOUTVCURUOUSVBNULUNBLSUAUDUEUFUGUH
    $.
    $( [2-Sep-2009] $)
$}

${
  $( The predicate "is a nonnegative real". $)
  elnnr $p |- ( X e. ( 0 [,) +oo ) <-> ( X e. RR /\ 0 <_ X ) ) $=
    ( cc0 cpnf cico co wcel cxr cle wbr clt w3a cr wa wb 0re rexr ax-mp pnfxr
    elico1 mp2an cmnf mnflt wi mnfxr xrltletr mp3an12 mpani anim1d xrrebnd
    sylibrd 3impib 3simp2 jca adantr pm3.27 ltpnf 3jca impbii bitri ) ABCDEFZAG
    FZBAHIZACJIZKZALFZVBMZBGFZCGFUTVDNBLFZVGOBPQZRBCASTVDVFVDVEVBVAVBVCVEVAVBVC
    MUAAJIZVCMVEVAVBVJVCVAUABJIZVBVJVHVKOBUBQUAGFVGVAVKVBMVJUCUDVIUABAUEUFUGUHA
    UIUJUKVAVBVCULUMVFVAVBVCVEVAVBAPUNVEVBUOVEVCVBAUPUNUQURUS $.
    $( [2-Sep-2009] $)
$}

