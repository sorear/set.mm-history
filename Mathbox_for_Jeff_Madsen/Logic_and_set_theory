$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Logic and set theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    anass1rs.1 $e |- ( ( ph /\ ( ps /\ ch ) ) -> th ) $.
    $( Commutative-associative law for conjunction in an antecedent.
       (Contributed by Jeff Madsen, 19-Jun-2011.) $)
    anass1rs $p |- ( ( ( ph /\ ch ) /\ ps ) -> th ) $=
      ( anassrs an32s ) ABCDABCDEFG $.
      $( [19-Jun-2011] $)
  $}

  ${
    anim12da.1 $e |- ( ( ph /\ ps ) -> th ) $.
    anim12da.2 $e |- ( ( ph /\ ch ) -> ta ) $.
    $( Conjoin antecedents and consequents in a deduction.  (Contributed by
       Jeff Madsen, 16-Jun-2011.) $)
    anim12da $p |- ( ( ph /\ ( ps /\ ch ) ) -> ( th /\ ta ) ) $=
      ( wa adantrr adantrl jca ) ABCHHDEABDCFIACEBGJK $.
      $( [16-Jun-2011] $)
  $}

  ${
    biadan2OLD.1 $e |- ( ph -> ps ) $.
    biadan2OLD.2 $e |- ( ps -> ( ph <-> ch ) ) $.
    $( Add a conjunction to an equivalence.  (Moved to ~ biadan2 in main set.mm
       and may be deleted by mathbox owner, JM. --NM 25-Feb-2014.) $)
    biadan2OLD $p |- ( ph <-> ( ps /\ ch ) ) $=
      ( biadan2 ) ABCDEF $.
      $( [20-Jun-2011] $)
  $}

  ${
    syldanl.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    syldanl.2 $e |- ( ( ( ph /\ ch ) /\ th ) -> ta ) $.
    $( A syllogism deduction with conjoined antecedents.  (Contributed by Jeff
       Madsen, 20-Jun-2011.) $)
    syldanl $p |- ( ( ( ph /\ ps ) /\ th ) -> ta ) $=
      ( wa ex imdistani sylan ) ABHACHDEABCABCFIJGK $.
      $( [20-Jun-2011] $)
  $}

  ${
    syl3an2cOLD.1 $e |- ( ( ph /\ ( ps /\ ch /\ th ) ) -> ta ) $.
    syl3an2cOLD.2 $e |- ( et -> ph ) $.
    syl3an2cOLD.3 $e |- ( et -> ps ) $.
    syl3an2cOLD.4 $e |- ( et -> ch ) $.
    syl3an2cOLD.5 $e |- ( et -> th ) $.
    $( A syllogism inference combined with contraction.  (Moved to
       ~ syl3an2cOLD in main set.mm and may be deleted by mathbox owner, JM.
       --NM 8-Sep-2011.) $)
    syl3an2cOLDOLD $p |- ( et -> ta ) $=
      ( syl13anc ) FABCDEHIJKGL $.
      $( [17-Jun-2011] $)
  $}

  ${
    mp4an.1OLD $e |- ph $.
    mp4an.2OLD $e |- ps $.
    mp4an.3OLD $e |- ch $.
    mp4an.4OLD $e |- th $.
    mp4an.5OLD $e |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) -> ta ) $.
    $( An inference based on modus ponens.  (Moved to ~ mp4an in main set.mm
       and may be deleted by mathbox owner, JM. --NM 19-Oct-2012.) $)
    mp4anOLD $p |- ta $=
      ( mp4an ) ABCDEFGHIJK $.
      $( [15-Jun-2010] $)
  $}

  ${
    imdistandaOLD.1 $e |- ( ( ph /\ ps ) -> ( ch -> th ) ) $.
    $( Distribution of implication with conjunction (deduction version with
       conjoined antecedent).  (Moved into main set.mm as ~ imdistanda and may
       be deleted by mathbox owner, SF. --NM 20-Sep-2013.) $)
    imdistandaOLD $p |- ( ph -> ( ( ps /\ ch ) -> ( ps /\ th ) ) ) $=
      ( imdistanda ) ABCDEF $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d ph x y $.  $d A y $.
    r19.21aivva.1OLD $e |- ( ( ph /\ ( x e. A /\ y e. B ) ) -> ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version with double quantification.)  (Moved to ~ ralrimivva
       in main set.mm and may be deleted by mathbox owner, JM. --NM
       17-Jul-2012.) $)
    r19.21aivvaOLD $p |- ( ph -> A. x e. A A. y e. B ps ) $=
      ( ralrimivva ) ABCDEFGH $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d w x z $.  $d w y z $.  $d w ph $.
    $( Substitution into "at most one".  (Moved to ~ sbmo in main set.mm and
       may be deleted by mathbox owner, JM. --NM 29-Jan-2012.) $)
    sbmoOLD $p |- ( [ y / x ] E* z ph <-> E* z [ y / x ] ph ) $=
      ( sbmo ) ABCDE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d ph y $.  $d ps x $.  $d A y $.  $d B x $.  $d x y $.
    $( Distribute quantification over "or".  (Moved to ~ 2ralor in main set.mm
       and may be deleted by mathbox owner, JM. --NM 29-Jan-2012.) $)
    2ralorOLD $p |- ( A. x e. A A. y e. B ( ph \/ ps ) <->
                  ( A. x e. A ph \/ A. y e. B ps ) ) $=
      ( 2ralor ) ABCDEFG $.
      $( [19-Jun-2010] $)
  $}

  $( Restricted existential quantification over union.  (Moved to ~ rexun in
     main set.mm and may be deleted by mathbox owner, JM. --NM 29-Jan-2012.) $)
  rexunOLD $p |- ( E. x e. ( A u. B ) ph <->
                          ( E. x e. A ph \/ E. x e. B ph ) ) $=
    ( rexun ) ABCDE $.
    $( [5-Jan-2011] $)

  ${
    $d A x y $.  $d B x y $.  $d ph y $.
    $( Restricted quantification over union.  (Moved to ~ ralun in main set.mm
       and may be deleted by mathbox owner, JM. --NM 29-Jan-2012.) $)
    ralunOLD $p |- ( ( A. x e. A ph /\ A. x e. B ph )
                                            -> A. x e. ( A u. B ) ph ) $=
      ( ralun ) ABCDE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d ps x $.
    rexsnOLD.1 $e |- A e. _V $.
    rexsnOLD.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted existential quantification over a singleton.  (Moved to
       ~ rexsn in main set.mm and may be deleted by mathbox owner, JM. --NM
       29-Jan-2012.) $)
    rexsnOLD $p |- ( E. x e. { A } ph <-> ps ) $=
      ( rexsn ) ABCDEFG $.
      $( [5-Jan-2011] $)
  $}

  ${
    $d A x $.  $d x y $.  $d ph x $.
    $( Specialized existential commutation lemma.  (Moved to ~ rexcom4a in main
       set.mm and may be deleted by mathbox owner, JM. --NM 16-Apr-2012.) $)
    rexcom4aOLD $p |- ( E. x E. y e. A ( ph /\ ps )
                          <-> E. y e. A ( ph /\ E. x ps ) ) $=
      ( rexcom4a ) ABCDEF $.
      $( [1-Jun-2011] $)

    $d B x $.
    rexcom4bOLD.1 $e |- B e. _V $.
    $( Specialized existential commutation lemma.  (Moved to ~ rexcom4b in main
       set.mm and may be deleted by mathbox owner, JM. --NM 16-Apr-2012.) $)
    rexcom4bOLD $p |- ( E. x E. y e. A ( ph /\ x = B ) <-> E. y e. A ph ) $=
      ( rexcom4b ) ABCDEFG $.
      $( [1-Jun-2011] $)
  $}

  ${
    $d ph y z $.  $d ps x z $.  $d ch x y $.  $d A y $.  $d B x z $.
    $d C x y $.
    $( Rearrange three existential quantifiers.  (Moved to ~ 3reeanv in main
       set.mm and may be deleted by mathbox owner, JM. --NM 21-Mar-2013.) $)
    3reeanvOLD $p |- ( E. x e. A E. y e. B E. z e. C ( ph /\ ps /\ ch )
                      <-> ( E. x e. A ph /\ E. y e. B ps /\ E. z e. C ch ) ) $=
      ( 3reeanv ) ABCDEFGHIJ $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d B x y $.  $d A x y $.  $d ph y $.  $d ps x y $.
    morexOLD.1 $e |- B e. _V $.
    morexOLD.2 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( Derive membership from uniqueness.  (Moved to ~ morex in main set.mm and
       may be deleted by mathbox owner, JM. --NM 19-Mar-2013.) $)
    morexOLD $p |- ( ( E. x e. A ph /\ E* x ph ) -> ( ps -> B e. A ) ) $=
      ( morex ) ABCDEFGH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d ps x $.
    euuni2OLD.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( The unique element such that ` ph ` .  (Moved to ~ euuni2 in main set.mm
       and may be deleted by mathbox owner, JM. --NM 15-Oct-2012.) $)
    euuni2OLD $p |- ( ( A e. B /\ E! x ph ) -> ( ps <-> U. { x | ph } = A ) )
        $=
      ( euuni2 ) ABCDEFG $.
      $( [1-Jun-2011] $)
  $}

  ${
    $d A w x y z $.  $d B w x z $.  $d C w x y $.  $d D w x y $.  $d F w x y $.
    $d ph w x z $.  $d ps w x y $.  $d ch w x y $.
    unirep.1 $e |- ( y = D -> ( ph <-> ps ) ) $.
    unirep.2 $e |- ( y = D -> B = C ) $.
    unirep.3 $e |- ( y = z -> ( ph <-> ch ) ) $.
    unirep.4 $e |- ( y = z -> B = F ) $.
    unirep.5 $e |- B e. _V $.
    $( Define a quantity whose definition involves a choice of representative,
       but which is uniquely determined regardless of the choice.  (Contributed
       by Jeff Madsen, 1-Jun-2011.) $)
    unirep $p |- ( ( A. y e. A A. z e. A ( ( ph /\ ch ) -> B = F ) /\
            ( D e. A /\ ps ) ) -> U. { x | E. y e. A ( ph /\ x = B ) } = C ) $=
      ( wa wceq wi cvv vw wral wcel wrex cv cab cuni eqidd ancli eqeq2d anbi12d
      rcla4ev sylan2 adantl weu wb csb wal ax-17 csbiegf ax-gen csbexg eqeltrrd
      a1i mpan2 ad2antrl wex eqeq1 anbi2d rexbidv cla4egv syl adantr mpd r19.29
      an4 pm3.35 eqeq12 syl5ibrcom ancoms expimpd ancomsd expdimp rexlimivw imp
      syl5bi sylan an32s ex alrimivv cbvrexv syl6bb eu4 sylanbrc syl11anc mpbid
      euuni2 ) ACQZHKRZSZFGUBZEGUBZJGUCZBQZQZAIHRZQZEGUDZADUEZHRZQZEGUDZDUFUGIR
      ZXDXHXBBXCBIIRZQZXHBXNBIUHUIXGXOEJGEUEZJRZABXFXNLXQHIIMUJUKULUMZUNXEITUCZ
      XLDUOZXHXMUPXCXSXBBXCEJHUQZITEUAJHIGUAUEZIUCZYCEURSXCYCEUSVDMUTXCHTUCZEUR
      YATUCYDEPVAEJHGTVBVEVCZVFXEXLDVGZXLCYBKRZQZFGUDZQXIYBRZSZUAURDURXTXDYFXBX
      DXHYFXRXCXHYFSZBXCXSYLYEXLXHDITXIIRZXKXGEGYMXJXFAXIIHVHVIVJZVKVLVMVNUNXEY
      KDUAXBYKXDXBXLYIYJXBXLQXAXKQZEGUDYIYJSZXAXKEGVOYOYPEGYOYIYJXAYIXKYJXAYIQW
      TYHQZFGUDZXKYJWTYHFGVOYRXKYJYQXKYJSFGWTYHXKYJWTXKYHYJXKYHQWRXJYGQZQWTYJAX
      JCYGVPWTWRYSYJWRWTYSYJSWRWTQYJYSWSWRWSVQXIHYBKVRVSVTWAWFWBWCWDWEWGWHWIWDV
      LWAVMWJXLYIDUAYJXLAYBHRZQZEGUDYIYJXKUUAEGYJXJYTAXIYBHVHVIVJUUAYHEFGXPFUER
      ZACYTYGNUUBHKYBOUJUKWKWLWMWNXLXHDITYNWQWOWP $.
      $( [1-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d ph x $.
    rabeq12.1OLD $e |- ( ph -> A = B ) $.
    rabeq12.2OLD $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equality of restricted class abstractions.  (Moved to ~ rabeqbidv in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       15-Sep-2011.) $)
    rabeq12OLD $p |- ( ph -> { x e. A | ps } = { x e. B | ch } ) $=
      ( rabeqbidv ) ABCDEFGHI $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d A x $.
    $( Condition for a restricted class abstraction to be empty.  (Moved to
       ~ rabeq0 in main set.mm and may be deleted by mathbox owner, JM. --NM
       1-Apr-2013.) $)
    rabeq0OLD $p |- ( { x e. A | ph } = (/) <-> A. x e. A -. ph ) $=
      ( rabeq0 ) ABCD $.
      $( [7-Jun-2010] $)
  $}

  ${
    $d A x $.
    $( Law of excluded middle, in terms of restricted class abstractions.
       (Contributed by Jeff Madsen, 20-Jun-2011.) $)
    rabxm $p |- A = ( { x e. A | ph } u. { x e. A | -. ph } ) $=
      ( wn wo crab cun wceq rabid2 cv wcel exmid a1i mprgbir unrab eqtr4i ) CAA
      DZEZBCFZABCFQBCFGCSHRBCRBCIRBJCKALMNAQBCOP $.
      $( [20-Jun-2011] $)

    $( Law of noncontradiction, in terms of restricted class abstractions.
       (Contributed by Jeff Madsen, 20-Jun-2011.) $)
    rabnc $p |- ( { x e. A | ph } i^i { x e. A | -. ph } ) = (/) $=
      ( crab wn cin wa c0 inrab wceq rabeq0 cv wcel pm3.24 a1i mprgbir eqtri )
      ABCDAEZBCDFARGZBCDZHARBCITHJSEZBCSBCKUABLCMANOPQ $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d x y $.  $d ps y $.
    ralab.1OLD $e |- ( y = x -> ( ph <-> ps ) ) $.
    $( Universal quantification over a class abstraction.  (Moved to ~ ralab in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       20-Oct-2011.) $)
    ralabOLD $p |- ( A. x e. { y | ph } ch <-> A. x ( ps -> ch ) ) $=
      ( ralab ) ABCDEFG $.
      $( [10-Jun-2010] $)

    $d A x y $.  $d ph x $.
    $( Universal quantification over a restricted class abstraction.  (Moved to
       ~ ralrab in main set.mm and may be deleted by mathbox owner, JM. --NM
       20-Oct-2011.) $)
    ralrabOLD $p |- ( A. x e. { y e. A | ph } ch <-> A. x e. A ( ps -> ch ) )
        $=
      ( ralrab ) ABCDEFGH $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d ps x $.  $d A x $.  $d x y $.
    rexrabOLD.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Existential quantification over a class abstraction.  (Moved to ~ rexrab
       in main set.mm and may be deleted by mathbox owner, JM. --NM
       20-Mar-2013.) $)
    rexrabOLD $p |- ( E. y e. { x e. A | ph } ch <-> E. y e. A ( ps /\ ch ) )
        $=
      ( rexrab ) ABCDEFGH $.
      $( [17-Jun-2011] $)
  $}

  ${
    $d F x y $.  $d A x y $.  $d B x y $.  $d ph x $.  $d ps y $.
    raleqfnOLD.1 $e |- ( y = ( F ` x ) -> ( ph <-> ps ) ) $.
    $( Change the domain of quantification by a function.  (Moved to ~ raleqfn
       in main set.mm and may be deleted by mathbox owner, JM. --NM
       17-Mar-2013.) $)
    raleqfnOLD $p |- ( ( F Fn A /\ B C_ A ) ->
                              ( A. x e. B ps <-> A. y e. ( F " B ) ph ) ) $=
      ( raleqfn ) ABCDEFGHI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d ph x z $.  $d B x y z $.  $d A x z $.
    cover2.1 $e |- B e. _V $.
    cover2.2 $e |- A = U. B $.
    $( Two ways of expressing the statement "there is a cover of ` A ` by
       elements of ` B ` such that for each set in the cover, ` ph ` ."  Note
       that ` ph ` and ` x ` must be distinct.  (Contributed by Jeff Madsen,
       20-Jun-2010.) $)
    cover2 $p |- ( A. x e. A E. y e. B ( x e. y /\ ph )
                        <-> E. z e. ~P B ( U. z = A /\ A. y e. z ph ) ) $=
      ( cv wcel wa wrex wral cuni wceq cpw crab wss ssrab2 eleq2 elpw2 mpbir wb
      wal hbra1 uniss ax-mp sseli syl6eleqr ra4 elunirab syl6ibr impbid2 alrimi
      dfcleq sylibr unieq eqeq1d anbi1d ax-17 hbrab1 hbeq rabid simprbi ralrimi
      syl6bi biantrud bitr4d rcla4ev sylancr biimpar eluni2 sylib elpwi r19.29r
      expcom ssrexv sylan9r sylan imp sylan2 anassrs ralrimiva anasss rexlimiva
      wi ancom2s impbii ) BIZCIZJZAKZCFLZBEMZDIZNZEOZACWOMZKZDFPZLZWNACFQZWTJZX
      BNZEOZXAXCXBFRZACFSZXBFGUAUBWNWIXDJZWIEJZUCZBUDXEWNXJBWMBEUEWNXHXIXHWIFNZ
      EXDXKWIXFXDXKRXGXBFUFUGUHHUIWNXIWMXHWMBEUJACWIFUKULUMUNBXDEUOUPWSXEDXBWTW
      OXBOZWSXEWRKXEXLWQXEWRXLWPXDEWOXBUQURUSXLWRXEXLACWOCBBWOXBWIWOJCUTACBFVAV
      BXLWJWOJWJXBJZAWOXBWJTXMWJFJAACFVCVDVFVEVGVHVIVJWSWNDWTWOWTJZWRWQWNXNWRWQ
      WNXNWRKZWQKWMBEXOWQXIWMWQXIKZXOWKCWOLZWMXPWIWPJZXQWQXRXIWPEWITVKCWIWOVLVM
      XOXQWMXNWOFRZWRXQWMWFWOFVNWRXQWLCWOLZXSWMXQWRXTWKACWOVOVPWLCWOFVQVRVSVTWA
      WBWCWDWGWEWH $.
      $( [20-Jun-2010] $)
  $}

  ${
    $d ph b x z $.  $d B b x y z $.  $d A b x z $.
    cover2g.1 $e |- A = U. B $.
    $( Two ways of expressing the statement "there is a cover of ` A ` by
       elements of ` B ` such that for each set in the cover, ` ph ` ."  Note
       that ` ph ` and ` x ` must be distinct.  (Contributed by Jeff Madsen,
       21-Jun-2010.) $)
    cover2g $p |- ( B e. C -> ( A. x e. A E. y e. B ( x e. y /\ ph )
                        <-> E. z e. ~P B ( U. z = A /\ A. y e. z ph ) ) ) $=
      ( vb cv wcel wa wrex cuni wral wceq cpw unieq syl6eqr rexeq eqeq2d anbi1d
      raleqbidv pweq rexeqbidv visset eqid cover2 vtoclbg ) BJCJKALZCIJZMZBUKNZ
      ODJZNZUMPZACUNOZLZDUKQZMUJCFMZBEOUOEPZUQLZDFQZMIFGUKFPZULUTBUMEVDUMFNEUKF
      RHSZUJCUKFTUCVDURVBDUSVCUKFUDVDUPVAUQVDUMEUOVEUAUBUEABCDUMUKIUFUMUGUHUI
      $.
      $( [21-Jun-2010] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Two ways of saying that two classes are disjoint.  (Moved to ~ disjr in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       23-Mar-2013.) $)
    disjrOLD $p |- ( ( A i^i B ) = (/) <-> A. x e. B -. x e. A ) $=
      ( disjr ) ABCD $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A z $.  $d B z $.  $d C z $.
    $( Represent a set difference as an intersection with a larger difference.
       (Moved to ~ difin2 in main set.mm and may be deleted by mathbox owner,
       JM. --NM 31-Mar-2013.) $)
    difin2OLD $p |- ( A C_ C -> ( A \ B ) = ( ( C \ B ) i^i A ) ) $=
      ( difin2 ) ABCD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d D x y z $.
    prf.1 $e |- A e. _V $.
    prf.2 $e |- B e. _V $.
    prf.3 $e |- C e. _V $.
    prf.4 $e |- D e. _V $.
    $( A function with a domain of two elements.  (Moved to ~ funprOLD in main
       set.mm and may be deleted by mathbox owner, JM. --NM 26-Aug-2011.) $)
    prfunOLD $p |- ( A =/= B -> Fun { <. A , C >. , <. B , D >. } ) $=
      ( funpr ) ABCDEFI $.
      $( [20-Jun-2010] $)

    $( The value of a function with a domain of two elements.  (Moved to
       ~ fvpr1OLD in main set.mm and may be deleted by mathbox owner, JM. --NM
       3-Sep-2011.) $)
    prfv1OLD $p |- ( A =/= B -> ( { <. A , C >. , <. B , D >. } ` A ) = C ) $=
      ( fvpr1 ) ABCDEGI $.
      $( [20-Jun-2010] $)

    $( The value of a function with a domain of two elements.  (Moved to
       ~ fvpr2OLD in main set.mm and may be deleted by mathbox owner, JM. --NM
       3-Sep-2011.) $)
    prfv2OLD $p |- ( A =/= B -> ( { <. A , C >. , <. B , D >. } ` B ) = D ) $=
      ( fvpr2 ) ABCDFHI $.
      $( [20-Jun-2010] $)

    $( A function with a domain of two elements.  (Moved to ~ fpr in main
       set.mm and may be deleted by mathbox owner, JM. --NM 3-Sep-2011.) $)
    prfOLD $p |- ( A =/= B -> { <. A , C >. , <. B , D >. }
                                      : { A , B } --> { C , D } ) $=
      ( fpr ) ABCDEFGHI $.
      $( [20-Jun-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d ch x y $.
    brabg2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    brabg2.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    brabg2.3 $e |- R = { <. x , y >. | ph } $.
    brabg2.4 $e |- ( ch -> A e. C ) $.
    $( Relation by a binary relation abstraction.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    brabg2 $p |- ( B e. D -> ( A R B <-> ch ) ) $=
      ( wcel cvv wrel brabg com3l mpdi wbr copab relopab releqi brrelexi biimpd
      mpbir wi wa ex exbiri impbid ) GIOZFGJUAZCUMUNFPOZCFGJJQADEUBZQADEUCJUPMU
      DUGUEUOUMUNCUOUMUNCUHUOUMUIUNCABCDEFGPIJKLMRUFUJSTUMCFHOZUNNUQUMCUNUQUMUN
      CABCDEFGHIJKLMRUKSTUL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F x $.  $d A x $.  $d B x $.
    $( Preimage of an intersection.  (Moved to ~ inpreima in main set.mm and
       may be deleted by mathbox owner, JM. --NM 28-Mar-2013.) $)
    inpreimaOLD $p |- ( Fun F -> ( `' F " ( A i^i B ) )
                                  = ( ( `' F " A ) i^i ( `' F " B ) ) ) $=
      ( inpreima ) ABCD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F x $.  $d A x $.  $d B x $.
    $( Preimage of a union.  (Moved to ~ unpreima in main set.mm and may be
       deleted by mathbox owner, JM. --NM 29-Mar-2013.) $)
    unpreimaOLD $p |- ( Fun F -> ( `' F " ( A u. B ) )
                                  = ( ( `' F " A ) u. ( `' F " B ) ) ) $=
      ( unpreima ) ABCD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F z $.  $d A z $.  $d B z $.
    $( The preimage of a restricted function.  (Moved to ~ respreima in main
       set.mm and may be deleted by mathbox owner, JM. --NM 30-Mar-2013.) $)
    respreimaOLD $p |- ( Fun F -> ( `' ( F |` B ) " A )
                                    = ( ( `' F " A ) i^i B ) ) $=
      ( respreima ) ABCD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F x y $.  $d A x y $.  $d B x y $.  $d C x y $.
    $( Property of a surjective function.  (Moved to ~ foelrn in main set.mm
       and may be deleted by mathbox owner, JM. --NM 4-Feb-2013.) $)
    foelrnOLD $p |- ( ( F : A -onto-> B /\ C e. B )
                                  -> E. x e. A C = ( F ` x ) ) $=
      ( foelrn ) ABCDEF $.
      $( [4-Jan-2011] $)
  $}

  ${
    $d F x y z $.  $d G x y z $.  $d A y z $.  $d B x y z $.  $d C x y z $.
    $( If a composition of two functions is surjective, then the function on
       the left is surjective.  (Moved to ~ foco2 in main set.mm and may be
       deleted by mathbox owner, JM. --NM 18-Apr-2013.) $)
    foco2OLD $p |- ( ( F : B --> C /\ G : A --> B /\
                        ( F o. G ) : A -onto-> C ) -> F : B -onto-> C ) $=
      ( foco2 ) ABCDEF $.
      $( [16-Jun-2011] $)
  $}

  $( The image of a pair under a funtion.  (Moved to ~ fnimapr in main set.mm
     and may be deleted by mathbox owner, JM. --NM 19-Apr-2013.) $)
  fnimaprOLD $p |- ( ( F Fn A /\ B e. A /\ C e. A ) ->
                          ( F " { B , C } ) = { ( F ` B ) , ( F ` C ) } ) $=
    ( fnimapr ) ABCDE $.
    $( [6-Jan-2011] $)

  ${
    $d A x y $.  $d B x y $.  $d ch x y $.
    opelopab3.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopab3.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    opelopab3.3 $e |- ( ch -> A e. C ) $.
    $( Ordered pair membership in an ordered pair class abstraction, with a
       reduced hypothesis.  (Contributed by Jeff Madsen, 29-May-2011.) $)
    opelopab3 $p |- ( B e. D -> ( <. A , B >. e.
                            { <. x , y >. | ph } <-> ch ) ) $=
      ( wcel cop copab cvv wa syl anim1i ancoms cxp wrel wss relopab mpbi sseli
      df-rel opelxp1 elisset opelopabg pm5.21nd ) GIMZFGNZADEOZMZCFPMZULQZUOULU
      QUOUPULUOUMPPUAZMUPUNURUMUNUBUNURUCADEUDUNUGUEUFFGPPUHRSTCULUQCUPULCFHMUP
      LFHUIRSTABCDEFGPIJKUJUK $.
      $( [29-May-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d G x y $.  $d H x y $.
    $( Cancellation of a surjective function from the right side of a
       composition.  (Contributed by Jeff Madsen, 1-Jun-2011.) $)
    cocanfo $p |- ( ( ( F : A -onto-> B /\ G Fn B /\ H Fn B )
                                    /\ ( G o. F ) = ( H o. F ) ) -> G = H ) $=
      ( vx vy wfn w3a ccom wceq wa cv cfv wral wi wcel wfun fnfun adantr wfo wf
      wrex dffo3 fveq1 cdm ffun fdm eleq2d biimpar 3expb syl2an anassrs ancom1s
      jca fvco 3adantl3 3adantl2 biimpa sylan2 an32s fveq2 syl5ibrcom rexlimdva
      eqeq12d 3exp1 3imp2 ralimdv ex com23 3expd sylbi 3imp1 wb eqfnfv2 3adant1
      imp mpbird ) ABCUAZDBHZEBHZIZDCJZECJZKZLDEKZFMZDNZWGENZKZFBOZVSVTWAWEWKVS
      ABCUBZWGGMZCNZKZGAUCZFBOZLZVTWAWEWKPPPGFABCUDWRVTWAWEWKWLWQVTWAWEIZWKPWLW
      SWQWKWLWSWQWKPWLWSLWPWJFBWLVTWAWEWPWJPZWLVTWAWEWTWLVTWAIZWELZWOWJGAXBWMAQ
      ZLWJWOWNDNZWNENZKZXAXCWEXFWEXAXCLZWMWCNZWMWDNZKZXFWMWCWDUEXGXJXFXGXHXDXIX
      EWLVTXCXHXDKZWAVTWLXCXKVTWLXCXKVTDRZCRZWMCUFZQZLZXKWLXCLZBDSXQXMXOWLXMXCA
      BCUGTWLXOXCWLXNAWMABCUHUIUJUOZXLXMXOXKWMDCUPUKULUMUNUQWLWAXCXIXEKZVTWAWLX
      CXSWAWLXCXSWAERZXPXSXQBESXRXTXMXOXSWMECUPUKULUMUNURVEUSUTVAWOWHXDWIXEWGWN
      DVBWGWNEVBVEVCVDVFVGVHVIVJVQVKVLVMWBWFWKVNZWEVTWAYAVSFBDEVOVPTVR $.
      $( [1-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $d D x $.
    $( Difference of Cartesian products, expressed in terms of a union of
       Cartesian products of differences.  (Moved to ~ difxp in main set.mm and
       may be deleted by mathbox owner, JM. --NM 15-Oct-2012.) $)
    difxpOLD $p |- ( ( C X. D ) \ ( A X. B ) ) =
                        ( ( ( ( C \ A ) X. D ) u. ( C X. ( D \ B ) ) )
                                          u. ( ( C \ A ) X. ( D \ B ) ) ) $=
      ( difxp ) ABCDE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A a b $.  $d B b $.  $d C a b c d $.  $d D a b d $.  $d Y a b $.
    $d Z a b $.
    $( Equinumerosity law for cross product.  (Moved to ~ xpeng in main set.mm
       and may be deleted by mathbox owner, JM. --NM 16-Mar-2013.) $)
    xpengOLD $p |- ( ( ( A e. W /\ B e. X ) /\ ( C e. Y /\ D e. Z ) ) ->
                    ( ( A ~~ B /\ C ~~ D ) -> ( A X. C ) ~~ ( B X. D ) ) ) $=
      ( wcel cen wbr wa cxp wi xpeng ad2ant2l ) BFIDHIABJKCDJKLACMBDMJKNAEICGIA
      BCDFHOP $.
      $( [17-Jun-2010] $)
  $}

  $( Move a conditional outside of a function.  (Contributed by Jeff Madsen,
     2-Sep-2009.)  (Moved to ~ fvif in main set.mm and may be deleted by
     mathbox owner, JM. --NM 16-Jan-2014.) $)
  fvifOLD $p |- ( F ` if ( ph , A , B ) ) = if ( ph , ( F ` A ) , ( F ` B ) )
      $=
    ( fvif ) ABCDE $.
    $( [2-Sep-2009] $)

  ${
    ifeq1daOLD.1 $e |- ( ( ph /\ ps ) -> A = B ) $.
    $( Conditional equality.  (Contributed by Jeff Madsen, 2-Sep-2009.)  (Moved
       to ~ ifeq1da in main set.mm and may be deleted by mathbox owner, JM.
       --NM 16-Jan-2014.) $)
    ifeq1daOLD $p |- ( ph -> if ( ps , A , C ) = if ( ps , B , C ) ) $=
      ( ifeq1da ) ABCDEFG $.
      $( [2-Sep-2009] $)
  $}

  ${
    ifeq2daOLD.1 $e |- ( ( ph /\ -. ps ) -> A = B ) $.
    $( Conditional equality.  (Contributed by Jeff Madsen, 2-Sep-2009.)  (Moved
       to ~ ifeq2da in main set.mm and may be deleted by mathbox owner, JM.
       --NM 16-Jan-2014.) $)
    ifeq2daOLD $p |- ( ph -> if ( ps , C , A ) = if ( ps , C , B ) ) $=
      ( ifeq2da ) ABCDEFG $.
      $( [2-Sep-2009] $)
  $}

  ${
    ifcldaOLD.1 $e |- ( ( ph /\ ps ) -> A e. C ) $.
    ifcldaOLD.2 $e |- ( ( ph /\ -. ps ) -> B e. C ) $.
    $( Conditional closure.  (Contributed by Jeff Madsen, 2-Sep-2009.)  (Moved
       to ~ ifclda in main set.mm and may be deleted by mathbox owner, JM. --NM
       15-Jan-2014.) $)
    ifcldaOLD $p |- ( ph -> if ( ps , A , B ) e. C ) $=
      ( ifclda ) ABCDEFGH $.
      $( [2-Sep-2009] $)
  $}

  ${
    xpeq1dOLD.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for cross product.  (Moved to ~ xpeq1d in main set.mm
       and may be deleted by mathbox owner, JM. --NM 4-Feb-2013.) $)
    xpeq1dOLD $p |- ( ph -> ( A X. C ) = ( B X. C ) ) $=
      ( xpeq1d ) ABCDEF $.
      $( [17-Jun-2010] $)

    $( Equality deduction for cross product.  (Moved to ~ xpeq1d in main set.mm
       and may be deleted by mathbox owner, JM. --NM 4-Feb-2013.) $)
    xpeq2dOLD $p |- ( ph -> ( C X. A ) = ( C X. B ) ) $=
      ( xpeq2d ) ABCDEF $.
      $( [17-Jun-2010] $)
  $}

  ${
    resexOLD.1 $e |- A e. _V $.
    $( The restriction of a set is a set.  (Contributed by Jeff Madsen,
       19-Jun-2011.)  (Moved to ~ resex in main set.mm and may be deleted by
       mathbox owner, JM. --NM 16-Jan-2014.) $)
    resexOLD $p |- ( A |` B ) e. _V $=
      ( resex ) ABCD $.
      $( [19-Jun-2011] $)
  $}

  ${
    brresi.1 $e |- B e. _V $.
    $( Restriction of a binary relation.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    brresi $p |- ( A ( R |` C ) B -> A R B ) $=
      ( cres resss ssbri ) DCFDABDCGH $.
      $( [2-Sep-2009] $)
  $}

  ${
    fconst6.1 $e |- B e. C $.
    $( A constant function as a mapping.  (Contributed by Jeff Madsen,
       30-Nov-2009.) $)
    fconst6 $p |- ( A X. { B } ) : A --> C $=
      ( csn cxp wf wss elisseti fconst wcel snssi ax-mp fss mp2an ) ABEZAPFZGPC
      HZACQGABBCDIJBCKRDBCLMAPCQNO $.
      $( [30-Nov-2009] $)
  $}

  ${
    $d A x y v w z $.  $d ph v w z $.
    opabex3OLD.1 $e |- A e. _V $.
    opabex3OLD.2 $e |- ( x e. A -> { y | ph } e. _V ) $.
    $( Existence of an ordered pair abstraction.  (Moved to ~ opabex3 in main
       set.mm and may be deleted by mathbox owner, JM. --NM 30-Jan-2013.) $)
    opabex3OLD $p |- { <. x , y >. | ( x e. A /\ ph ) } e. _V $=
      ( opabex3 ) ABCDEFG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d ph x $.  $d ph y $.
    fnopabeqd.1 $e |- ( ph -> B = C ) $.
    $( Equality deduction for function abstractions.  (Contributed by Jeff
       Madsen, 19-Jun-2011.) $)
    fnopabeqd $p |- ( ph -> { <. x , y >. | ( x e. A /\ y = B ) }
                          = { <. x , y >. | ( x e. A /\ y = C ) } ) $=
      ( cv wcel wceq wa eqeq2d anbi2d opabbidv ) ABHDIZCHZEJZKOPFJZKBCAQROAEFPG
      LMN $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A d r x y $.  $d B d r y $.  $d C d r x y $.  $d D d r x y $.
    $d R r x y $.  $d d x y $.  $d r x y $.
    fvopabf4g.1 $e |- C e. _V $.
    fvopabf4g.2 $e |- ( x = A -> B = C ) $.
    fvopabf4g.3 $e |- F = { <. x , y >. | ( x : D --> R /\ y = B ) } $.
    $( Function value of an operator abstraction whose domain is a set of
       functions with given domain and range.  (Contributed by Jeff Madsen,
       1-Dec-2009.) $)
    fvopabf4g $p |- ( ( D e. X /\ R e. Y /\ A : D --> R ) -> ( F ` A ) = C ) $=
      ( vd vr wf cfv cv wceq wa wcel w3a copab fveq1i wi anbi1d opabbidv fveq1d
      feq2 eqeq1d imbi12d feq3 visset eqid fvopabf4 vtocl2g 3impia syl5eq ) FIU
      AZGJUAZFGCPZUBCHQCFGARZPZBRDSZTZABUCZQZECHVFMUDUSUTVAVGESZNRZORZCPZCVIVJV
      BPZVDTZABUCZQZESZUEFVJCPZCFVJVBPZVDTZABUCZQZESZUEVAVHUENOFGIJVIFSZVKVQVPW
      BVIFVJCUIWCVOWAEWCCVNVTWCVMVSABWCVLVRVDVIFVJVBUIUFUGUHUJUKVJGSZVQVAWBVHVJ
      GFCULWDWAVGEWDCVTVFWDVSVEABWDVRVCVDVJGFVBULUFUGUHUJUKABCDEVIVJVNKNUMOUMLV
      NUNUOUPUQUR $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d F x $.  $d G x $.  $d A x $.  $d B x $.
    $( Two functions on ` A u. B ` are equal if and only if they have equal
       restrictions to both ` A ` and ` B ` .  (Contributed by Jeff Madsen,
       19-Jun-2011.) $)
    eqfnun $p |- ( ( F Fn ( A u. B ) /\ G Fn ( A u. B ) ) -> ( F = G <->
                ( ( F |` A ) = ( G |` A ) /\ ( F |` B ) = ( G |` B ) ) ) ) $=
      ( vx cun wfn wa wceq cres reseq1 jca cfv wcel fveq1 adantr adantl 3eqtr3d
      cv fvres wral wo adantlr adantll jaodan sylan2b ralrimiva eqfnfv2 syl5ibr
      elun impbid2 ) CABFZGDULGHZCDIZCAJZDAJZIZCBJZDBJZIZHZUNUQUTCDAKCDBKLVAUNU
      MESZCMZVBDMZIZEULUAVAVEEULVBULNVAVBANZVBBNZUBVEVBABUJVAVFVEVGUQVFVEUTUQVF
      HVBUOMZVBUPMZVCVDUQVHVIIVFVBUOUPOPVFVHVCIUQVBACTQVFVIVDIUQVBADTQRUCUTVGVE
      UQUTVGHVBURMZVBUSMZVCVDUTVJVKIVGVBURUSOPVGVJVCIUTVBBCTQVGVKVDIUTVBBDTQRUD
      UEUFUGEULCDUHUIUK $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d ph c $.  $d ps x $.  $d ch x y $.  $d th x y z $.  $d ta x y c $.
    $d R x y z c $.  $d S x y z c $.  $d A x y z c $.  $d B x y z c $.
    $d C x y z c $.
    oprabvalg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    oprabvalg.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    oprabvalg.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    oprabvalg.4 $e |- ( ( ta /\ ( x e. R /\ y e. S ) ) -> E! z ph ) $.
    oprabvalg.5 $e |- F = { <. <. x , y >. , z >. |
                                            ( ( x e. R /\ y e. S ) /\ ph ) } $.
    $( The value of an operation class abstraction.  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    oprabvalg $p |- ( ( ta /\ ( A e. R /\ B e. S /\ C e. D ) )
                                            -> ( ( A F B ) = C <-> th ) ) $=
      ( vc wcel w3a wa co cop cv copab2 cfv df-opr fveq1i eqtri eqeq1i wb eqeq2
      wi opeq2 eleq1d bibi12d imbi2d copab wfn weu wal ex alrimivv fnoprabg syl
      wceq eleq1 anbi1d anbi2d opelopabg ibir visset fnopfvb syl2an com12 exp32
      vtoclg 3imp2 anbi12d eloprabg adantl bitrd syl5bb biidd bianabs 3adant3 )
      EIMUBZJNUBZKLUBZUCZUDZIJOUEZKVIZWJWKUDZDUDZDWPIJUFZFUGZMUBZGUGZNUBZUDZAUD
      ZFGHUHZUIZKVIZWNWRWOXGKWOWSOUIXGIJOUJWSOXFTUKULUMWNXHWSKUFZXFUBZWREWJWKWL
      XHXJUNZEWJWKWLXKUPWLEWQUDZXKXLXGUAUGZVIZWSXMUFZXFUBZUNZUPXLXKUPUAKLXMKVIZ
      XQXKXLXRXNXHXPXJXMKXGUOXRXOXIXFXMKWSUQURUSUTEXFXDFGVAZVBZWSXSUBZXQWQEXDAH
      VCZUPZGVDFVDXTEYCFGEXDYBSVEVFXDAFGHVGVHWQYAXDWJXCUDZWQFGIJMNWTIVIZXAWJXCW
      TIMVJVKZXBJVIZXCWKWJXBJNVJVLZVMVNXSWSXMXFUAVOVPVQVTVRVSWAWMXJWRUNEXEYDBUD
      WQCUDWRFGHIJKMNLYEXDYDABYFPWBYGYDWQBCYHQWBHUGKVICDWQRVLWCWDWEWFWMWRDUNZEW
      JWKYIWLWQWRDWQWRWGWHWIWDWE $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d v w x y z $.  $d ph v $.  $d ps v $.
    cbvoprab2.1 $e |- ( ph -> A. w ph ) $.
    cbvoprab2.2 $e |- ( ps -> A. y ps ) $.
    cbvoprab2.3 $e |- ( y = w -> ( ph <-> ps ) ) $.
    $( Change the second bound variable in an operation abstraction.
       (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    cbvoprab2 $p |- { <. <. x , y >. , z >. | ph } =
                    { <. <. x , w >. , z >. | ps } $=
      ( vv cv cop wceq wa wex cab copab2 ax-17 hban hbex opeq1d eqeq2d df-oprab
      opeq2 anbi12d exbidv cbvex exbii abbii 3eqtr4i ) JKZCKZDKZLZEKZLZMZANZEOZ
      DOZCOZJPUKULFKZLZUOLZMZBNZEOZFOZCOZJPACDEQBCFEQVAVIJUTVHCUSVGDFURFEUQAFUQ
      FRGSTVFDEVEBDVEDRHSTUMVBMZURVFEVJUQVEABVJUPVDUKVJUNVCUOUMVBULUDUAUBIUEUFU
      GUHUIACDEJUCBCFEJUCUJ $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d A v x y z w $.  $d ph v $.
    oprabrexex2.1 $e |- A e. _V $.
    oprabrexex2.2 $e |- { <. <. x , y >. , z >. | ph } e. _V $.
    $( Existence of an existentially restricted operation abstraction.
       (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    oprabrexex2 $p |- { <. <. x , y >. , z >. | E. w e. A ph } e. _V $=
      ( vv wrex copab2 cv cop wa wex cab cvv df-oprab rexcom4 exbii wceq bitr2i
      r19.42v bitri abbii eqtri eqeltrri abrexex2 eqeltri ) AEFJZBCDKZILBLCLMDL
      MUAZANZDOZCOZBOZEFJZIPZQUKULUJNZDOZCOZBOZIPURUJBCDIRVBUQIUQUOEFJZBOVBUOEB
      FSVCVABVCUNEFJZCOVAUNECFSVDUTCVDUMEFJZDOUTUMEDFSVEUSDULAEFUCTUDTUDTUBUEUF
      UPEIFGABCDKUPIPQABCDIRHUGUHUI $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d C x y z $.  $d B y z $.  $d H x y z $.  $d A x y z $.  $d B z $.
    $d F z $.  $d G z $.
    fnopabco.1 $e |- ( x e. A -> B e. C ) $.
    fnopabco.2 $e |- F = { <. x , y >. | ( x e. A /\ y = B ) } $.
    fnopabco.3 $e |- G = { <. x , y >. | ( x e. A /\ y = ( H ` B ) ) } $.
    $( Composition of a function with a function abstraction.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    fnopabco $p |- ( H Fn C -> G = ( H o. F ) ) $=
      ( vz wfn wceq cv cfv ax-mp wcel cvv adantl ccom wral fvex fnopab2 crn wss
      wa wb wf fopab ffn frn fnco mp3an23 eqfnfv sylancr csb wsbc sbcth2 visset
      eqidd sbcel1g sylib ax-17 hbcsb1 csbeq1a fvopab4gf mpdan fveq2d cdm fnfun
      wfun adantr ffun a1i fdmi biimpri fvco syl111anc mpan2 3eqtr4rd ralrimiva
      eleq2i hbfv mpbir2and ) HEMZGHFUAZNZCCNZLOZGPZWJWGPZNZLCUBZWFGCMWGCMZWHWI
      WNUGUHABCDHPZGDHUCKUDWFFCMZFUEEUFZWOCEFUIZWQABCEDFJIUJZCEFUKQWSWRWTCEFULQ
      ECHFUMUNLCCGWGUOUPWFCVAWFWMLCWFWJCRZUGZWJFPZHPZAWJDUQZHPZWLWKXBXCXEHXAXCX
      ENZWFXAXEERZXGXADERZAWJURZXHXIAWJCIUSWJSRXJXHUHLUTZAWJDESVBQVCABBWJDXECEF
      BOZWJRAVDZABWJDXKXMVEZAWJDVFZJVGVHTVIXBHVLZFVLZWJFVJZRZWLXDNWFXPXAEHVKVMX
      QXBWSXQWTCEFVNQVOXAXSWFXSXAXRCWJCEFWTVPWCVQTWJHFVRVSXAWKXFNZWFXAXFSRXTXEH
      UCABBWJWPXFCSGXMABXEHXLHRAVDXNWDAOWJNDXEHXOVIKVGVTTWAWBWE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d C y $.  $d M x y $.  $d R x y $.  $d S x y $.
    opropabco.1 $e |- ( x e. A -> B e. R ) $.
    opropabco.2 $e |- ( x e. A -> C e. S ) $.
    opropabco.3 $e |- F = { <. x , y >. | ( x e. A /\ y = <. B , C >. ) } $.
    opropabco.4 $e |- G = { <. x , y >. | ( x e. A /\ y = ( B M C ) ) } $.
    $( Composition of an operator with a function abstraction.  (Contributed by
       Jeff Madsen, 11-Jun-2010.) $)
    opropabco $p |- ( M Fn ( R X. S ) -> G = ( M o. F ) ) $=
      ( cop cv wcel wceq wa copab cxp opelxpi syl11anc cfv df-opr eqeq2i anbi2i
      co opabbii eqtri fnopabco ) ABCDEOZFGUAZHIJAPCQZDFQEGQULUMQKLDEFGUBUCMIUN
      BPZDEJUHZRZSZABTUNUOULJUDZRZSZABTNURVAABUQUTUNUPUSUODEJUEUFUGUIUJUK $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d ph x y z $.
    oprabexdOLD.1 $e |- ( ph -> A e. _V ) $.
    oprabexdOLD.2 $e |- ( ph -> B e. _V ) $.
    oprabexdOLD.3 $e |- ( ( ph /\ ( x e. A /\ y e. B ) ) -> E* z ps ) $.
    oprabexdOLD.4 $e |- ( ph -> F = { <. <. x , y >. , z >. |
                                        ( ( x e. A /\ y e. B ) /\ ps ) } ) $.
    $( Existence of an operator abstraction.  (Moved to ~ oprabexd in main
       set.mm and may be deleted by mathbox owner, JM. --NM 16-Sep-2013.) $)
    oprabexdOLD $p |- ( ph -> F e. _V ) $=
      ( oprabexd ) ABCDEFGHIJKLM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A r s t u v w $.  $d B r s t u v w $.  $d F r s t u v w $.
    $( Condition for an operation to be one-to-one.  (Contributed by Jeff
       Madsen, 17-Jun-2010.) $)
    f1opr $p |- ( F : ( A X. B ) -1-1-> C <-> ( F : ( A X. B ) --> C /\
                          A. r e. A A. s e. B A. t e. A A. u e. B
                    ( ( r F s ) = ( t F u ) -> ( r = t /\ s = u ) ) ) ) $=
      ( vv vw cv cfv wceq wi wral wa co cop fveq2 visset cxp wf1 df-opr syl6eqr
      dff13 eqeq1d eqeq1 imbi12d ralbidv ralxp eqeq2d eqeq2 opth syl6bb 2ralbii
      wf bitri anbi2i ) CDUAZEFUBUSEFUPZIKZFLZJKZFLZMZVAVCMZNZJUSOZIUSOZPUTHKZG
      KZFQZBKZAKZFQZMZVJVMMVKVNMPZNZADOBCOZGDOHCOZPIJUSEFUEVIVTUTVIVLVDMZVJVKRZ
      VCMZNZJUSOZGDOHCOVTVHWEIHGCDVAWBMZVGWDJUSWFVEWAVFWCWFVBVLVDWFVBWBFLVLVAWB
      FSVJVKFUCUDUFVAWBVCUGUHUIUJWEVSHGCDWDVRJBACDVCVMVNRZMZWAVPWCVQWHVDVOVLWHV
      DWGFLVOVCWGFSVMVNFUCUDUKWHWCWBWGMVQVCWGWBULVJVKVMVNHTGTATUMUNUHUJUOUQURUQ
      $.
      $( [17-Jun-2010] $)
  $}

  $( Composition with the converse.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  cnvcan $p |- ( Fun G -> ( G o. `' G ) = ( _I |` ran G ) ) $=
    ( wfun ccnv ccom cid wss crn cres wceq wrel df-fun simprbi cdm dfdm4 dmcoeq
    iss ax-mp df-rn eqtr4i a1i reseq2d eqeq2d syl5bb mpbid ) ABZAACZDZEFZUGEAGZ
    HZIZUEAJUHAKLUHUGEUGMZHZIUEUKUGPUEUMUJUGUEULUIEULUIIUEULUFMZUIAMUFGIULUNIAN
    AUFOQARSTUAUBUCUD $.
    $( [2-Sep-2009] $)

  $( Composition with a function and then with the converse.  (Contributed by
     Jeff Madsen, 2-Sep-2009.) $)
  cocnv $p |- ( ( Fun F /\ Fun G ) ->
                              ( ( F o. G ) o. `' G ) = ( F |` ran G ) ) $=
    ( wfun wa ccom ccnv crn cres coass cid wceq cnvcan adantl coeq2d resco wrel
    funrel coi1 reseq1 3syl adantr syl5eqr eqtrd syl5eq ) ACZBCZDZABEBFZEABUHEZ
    EZABGZHZABUHIUGUJAJUKHZEZULUGUIUMAUFUIUMKUEBLMNUGUNAJEZUKHZULAJUKOUEUPULKZU
    FUEAPUOAKUQAQARUOAUKSTUAUBUCUD $.
    $( [2-Sep-2009] $)

  $( Cancel a composition by a bijection by preapplying the converse.
     (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  f1ocan1fv $p |- ( ( Fun F /\ G : A -1-1-onto-> B /\ X e. B ) ->
                            ( ( F o. G ) ` ( `' G ` X ) ) = ( F ` X ) ) $=
    ( wfun wf1o wcel w3a ccom ccnv cfv crn f1ofun wa sylan2 3adant3 syl 3adant1
    wceq cres cocnv fveq1d cdm funco f1ocnv 3ad2ant2 wf f1of fdm eleq2d biimpar
    sylan fvco syl111anc df-rn syl6eleqr fvres 3eqtr3d ) CFZABDGZEBHZIZECDJZDKZ
    JZLZECDMZUAZLZEVELVDLZECLZUTVAVGVJTZVBVAUTDFZVMABDNZUTVNOEVFVICDUBUCPQVCVDF
    ZVEFZEVEUDZHZVGVKTUTVAVPVBVAUTVNVPVOCDUEPQVAUTVQVBVABAVEGZVQABDUFZBAVENRUGV
    AVBVSUTVABAVEUHZVBVSVAVTWBWABAVEUIRWBVSVBWBVRBEBAVEUJUKULUMZSEVDVEUNUOVAVBV
    JVLTZUTVAVBOZEVHHWDWEEVRVHWCDUPUQEVHCURRSUS $.
    $( [2-Sep-2009] $)

  $( Cancel a composition by the converse of a bijection by preapplying the
     bijection.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  f1ocan2fv $p |- ( ( Fun F /\ G : A -1-1-onto-> B /\ X e. A ) ->
                            ( ( F o. `' G ) ` ( G ` X ) ) = ( F ` X ) ) $=
    ( wfun wf1o wcel w3a ccnv ccom wceq wrel f1ofun funrel dfrel2 3syl 3ad2ant2
    cfv biimpi fveq1d fveq2d f1ocnv f1ocan1fv syl3an2 eqtr3d ) CFZABDGZEAHZIZED
    JZJZSZCUKKZSZEDSZUNSECSZUJUMUPUNUJEULDUHUGULDLZUIUHDFDMZURABDNDOUSURDPTQRUA
    UBUHUGBAUKGUIUOUQLABDUCBACUKEUDUEUF $.
    $( [2-Sep-2009] $)

  ${
    $d F z $.  $d A z $.  $d Y z $.  $d X z $.  $d B z $.
    $( Membership in the image of a 1-1 map.  (Moved to ~ f1elima in main
       set.mm and may be deleted by mathbox owner, JM. --NM 16-Sep-2013.) $)
    f1elimaOLD $p |- ( ( F : A -1-1-> B /\ X e. A /\ Y C_ A )
                                -> ( ( F ` X ) e. ( F " Y ) <-> X e. Y ) ) $=
      ( f1elima ) ABCDEF $.
      $( [2-Sep-2009] $)
  $}

  $( A one-to-one mapping of finite sets with the same cardinality is
     bijective.  (Moved to ~ fienf1f1o in main set.mm and may be deleted by
     mathbox owner, JM. --NM 24-Sep-2013.) $)
  enf1f1oOLD $p |- ( ( A e. Fin /\ B ~~ A ) ->
                        ( F : A -1-1-> B -> F : A -1-1-onto-> B ) ) $=
    ( fienf1f1o ) ABCD $.
    $( [5-Jun-2010] $)

  ${
    $d A x $.  $d B x $.  $d F x $.  $d G x $.
    $( Derive equality of functions from equality of their values.  (Moved to
       ~ eqfnfv3 in main set.mm and may be deleted by mathbox owner, JM. --NM
       22-Nov-2011.) $)
    eqfnfv3OLD $p |- ( ( F Fn A /\ G Fn B ) -> ( F = G <-> ( B C_ A /\ A. x e.
        A
                                  ( x e. B /\ ( F ` x ) = ( G ` x ) ) ) ) ) $=
      ( eqfnfv3 ) ABCDEF $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x f $.  $d A f $.  $d B f $.  $d C f $.
    $( Intersection of Cartesian products over the same base set.  (Contributed
       by Jeff Madsen, 2-Sep-2009.) $)
    inixp $p |- ( X_ x e. A B i^i X_ x e. A C ) = X_ x e. A ( B i^i C ) $=
      ( vf cixp cin cv wfn cfv wcel wral wa an4 anidm r19.26 elin anbi12i elixp
      bicomi ralbii bitr3i bitri visset 3bitr4i ineqri ) EABCFZABDFZABCDGZFZEHZ
      BIZAHUKJZCKZABLZMZULUMDKZABLZMZMZULUMUIKZABLZMZUKUGKZUKUHKZMUKUJKUTULULMZ
      UOURMZMVCULUOULURNVFULVGVBULOVGUNUQMZABLVBUNUQABPVHVAABVAVHUMCDQTUAUBRUCV
      DUPVEUSABCUKEUDZSABDUKVISRABUIUKVISUEUF $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d f x y w $.  $d f x y z $.  $d A f x y $.  $d B f w $.  $d C f z $.
    cbvixp.1 $e |- ( w e. B -> A. y w e. B ) $.
    cbvixp.2 $e |- ( z e. C -> A. x z e. C ) $.
    cbvixp.3 $e |- ( x = y -> B = C ) $.
    $( Change bound variable in an indexed Cartesian product.  (Contributed by
       Jeff Madsen, 20-Jun-2011.) $)
    cbvixp $p |- X_ x e. A B = X_ y e. A C $=
      ( vf cv cfv wcel wral wa cab cixp ax-17 hbel wfn wceq fveq2 cbvral anbi2i
      eleq12d abbii df-ixp 3eqtr4i ) KLZEUAZALZUJMZFNZAEOZPZKQUKBLZUJMZGNZBEOZP
      ZKQAEFRBEGRUPVAKUOUTUKUNUSABEBDDUMFDLUMNBSHTACCURGCLURNASITULUQUBUMURFGUL
      UQUJUCJUFUDUEUGAEFKUHBEGKUHUI $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d A x y f $.  $d B y f z $.  $d C x f z $.
    cbvixpv.1 $e |- ( x = y -> B = C ) $.
    $( Change bound variable in an indexed Cartesian product.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    cbvixpv $p |- X_ x e. A B = X_ y e. A C $=
      ( vz cv wcel ax-17 cbvixp ) ABGGCDEGHZDIBJLEIAJFK $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d x y $.
    $( The index variable in an indexed cross product is not free.
       (Contributed by Jeff Madsen, 19-Jun-2011.) $)
    hbixp1 $p |- ( y e. X_ x e. A B -> A. x y e. X_ x e. A B ) $=
      ( cv cixp wcel wfn cfv wral wa visset elixp ax-17 hbra1 hban hbxfrbi ) BE
      ZACDFGRCHZAERIDGZACJZKAACDRBLMSUAASANTACOPQ $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f x $.  $d B f $.  $d C f $.  $d D f $.
    $( An infinite Cartesian product is a subset of set exponentiation.  (Moved
       into main set.mm as ~ ixpssmapg and may be deleted by mathbox owner, JM.
       --NM 18-Jun-2013.) $)
    ixpssmapgOLD $p |- ( ( A e. C /\ A. x e. A B e. D )
                                -> X_ x e. A B C_ ( U_ x e. A B ^m A ) ) $=
      ( ixpssmapg ) ABCDEF $.
      $( [19-Jun-2011] $)
  $}

  $( Set exponentiation of finite sets is finite.  (Moved into main set.mm as
     ~ mapfi and may be deleted by mathbox owner, JM. --NM 24-Sep-2013.) $)
  mapfiOLD $p |- ( ( A e. Fin /\ B e. Fin ) -> ( A ^m B ) e. Fin ) $=
    ( mapfi ) ABC $.
    $( [19-Jun-2011] $)

  ${
    $d A x $.
    $( A cross product of finitely many finite sets is finite.  (Moved into
       main set.mm as ~ ixpfi and may be deleted by mathbox owner, JM. --NM
       24-Sep-2013.) $)
    ixpfiOLD $p |- ( ( A e. Fin /\ A. x e. A B e. Fin )
             -> X_ x e. A B e. Fin ) $=
      ( ixpfi ) ABCD $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A a b c d h s t u v w x y z $.  $d R a b c d h s t u v w x y z $.
    $d S a b c d h s t u v w x y z $.  $d F a b c d h s t u v w x y z $.
    $d B a b c d h s t u v w x y z $.  $d C a b c d h s t u v w x y z $.
    $d X a c d h s t u v w x y z $.  $d P a c d h s t u v $.
    upixp.1 $e |- X = X_ b e. A ( C ` b ) $.
    upixp.2 $e |- P = { <. w , z >. | ( w e. A /\ z =
                          { <. x , y >. | ( x e. X /\ y = ( x ` w ) ) } ) } $.
    $( Universal property of the indexed Cartesian product.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    upixp $p |- ( ( A e. R /\ B e. S /\ A. a e. A
                  ( F ` a ) : B --> ( C ` a ) ) -> E! h ( h : B --> X
                        /\ A. a e. A ( F ` a ) = ( ( P ` a ) o. h ) ) ) $=
      ( wcel cfv wceq vu vv vs vt vc vd cv wf wral w3a ccom wa weu cvv opabex2g
      copab eueq sylib 3ad2ant2 wfn ffn ad2antrl adantr ralrimiva eqid fnopab2g
      3ad2ant1 eqfnfv syl11anc eqidd fveq2 eqeq2d anbi2d opabbidv sylan2 ancoms
      fvopab4g 3ad2antl1 adantlr fvex fnopab2 ffvelrn cixp eleq2i ciun ixpf syl
      wb adantll sylancr fveq1d fvopab4 adantl coeq1d eqeq12d rcla4cva wfun cdm
      sylbi fnfun ax-mp rgen ixpexg mpan2 syl5eqel ad2antrr sylan funeqd mpbiri
      a1i ffun fdm eleq2d biimpar syl111anc fveq1 3eqtrd mpbir2and eqtr2d feq1d
      fvco ex feq3 bitrd anasss ancom2s 3ad2antl3 anassrs eqeltrd elixp2 sylibr
      3jca syl6eleqr fopab2 crn wss fneq1d ad3antrrr an32s frn fnco fndm fveq2d
      ad2antlr eqtrd cbvralv 3adant2 jca feq1 ralbidv anbi12d syl5ibrcom impbid
      coeq2 eubidv mpbird ) EIRZFJRZFNUGZGSZUUSLSZUHZNEUIZUJZFMKUGZUHZUVAUUSHSZ
      UVEUKZTZNEUIZULZKUMUVEUAUGZFRZUBUGUCUGZERZUDUGZUVLUVNLSZSZTZULZUCUDUPZTUL
      UAUBUPZTZKUMZUURUUQUWDUVCUURUWBUNRUWDUAUBFUWAJUOKUWBUQURUSUVDUVKUWCKUVDUV
      KUWCUVDUVKUWCUVDUVKULZUWCFFTZUEUGZUVESZUWGUWBSZTZUEFUIZUWEUVEFUTZUWBFUTZU
      WCUWFUWKULWHUVFUWLUVDUVJFMUVEVAVBUVDUWMUVKUUQUURUWMUVCUUQUWAUNRZUAFUIUWMU
      UQUWNUAFUUQUWNUVMUCUDEUVRIUOZVCVDUAUBFUWAUWBUWBVEZVFURZVGVCUEFFUVEUWBVHVI
      UWEFVJUWEUWJUEFUWEUWGFRZULZUWIUVOUVPUWGUVQSZTZULZUCUDUPZUWHUVDUWRUWIUXCTZ
      UVKUUQUURUWRUXDUVCUWRUUQUXDUUQUWRUXCUNRUXDUCUDEUWTIUOUAUBUWGUWAUXCFUNUWBU
      VLUWGTZUVTUXBUCUDUXEUVSUXAUVOUXEUVRUWTUVPUVLUWGUVQVKVLVMVNUWPVQVOVPVRVSUW
      SUXCUWHTZEETZUFUGZUXCSZUXHUWHSZTZUFEUIZUWSUXCEUTUWHEUTZUXFUXGUXLULWHUCUDE
      UWTUXCUWGUVQVTUXCVEZWAUVKUWRUXMUVDUVFUWRUXMUVJUVFUWRULZUWHMRZUXMFMUWGUVEW
      BZUXPUWHOEOUGZGSZWCZRZUXMMUXTUWHPWDUYAEOEUXSWEZUWHUHUXMOEUXSUWHWFEUYBUWHV
      AWGWSWGVSWIUFEEUXCUWHVHWJUWSEVJUWSUXKUFEUWSUXHERZULZUXIUWGUXHLSZSZUWGUXHH
      SZUVEUKZSZUXJUYCUXIUYFTUWSUCUDUXHUWTUYFEUXCUVNUXHTUWGUVQUYEUVNUXHLVKWKUXN
      UWGUYEVTWLWMUYDUWGUYEUYHUWEUYCUYEUYHTZUWRUVKUYCUYJUVDUVJUYCUYJUVFUVIUYJNU
      XHEUUSUXHTZUVAUYEUVHUYHUUSUXHLVKUYKUVGUYGUVEUUSUXHHVKWNWOWPWIWIVSWKUYDUYI
      UWHUYGSZUWHAUGZMRZBUGZUXHUYMSZTZULZABUPZSZUXJUYDUYGWQZUVEWQZUWGUVEWRZRZUY
      IUYLTUYDVUAUYSWQZUYSMUTVUEABMUYPUYSUXHUYMVTUYSVEZWAMUYSWTXAUYDUYGUYSUWSUY
      SUNRZUYCUYGUYSTZUVDVUGUVKUWRUUQUURVUGUVCUUQMUNRZVUGUUQMUXTUNPUUQUXSUNRZOE
      UIUXTUNRVUJOEVUJUXRERZUXRGVTXJXBOEUXSIUNXCXDXEZABMUYPUNUOWGVGXFUYCVUGVUHD
      CUXHUYNUYODUGZUYMSZTZULZABUPZUYSEUNHVUMUXHTZVUPUYRABVURVUOUYQUYNVURVUNUYP
      UYOVUMUXHUYMVKVLVMVNQVQVPXGZXHXIUWEVUBUWRUYCUVFVUBUVDUVJFMUVEXKVBXFUWSVUD
      UYCUVKUWRVUDUVDUVFUWRVUDUVJUVFVUDUWRUVFVUCFUWGFMUVEXLXMXNVSWIVCUWGUYGUVEY
      AXOUYDUWHUYGUYSVUSWKUWSUYTUXJTZUYCUVKUWRVUTUVDUVFUWRVUTUVJUXOUXPVUTUXQABU
      WHUYPUXJMUYSUXHUYMUWHXPVUFUXHUWHVTWLWGVSWIVCXQXQVDXRXSVDXRYBUVDUVKUWCFMUW
      BUHZUVAUVGUWBUKZTZNEUIZULUVDVVAVVDUVDUWAMRZUAFUIZVVAUVDVVEUAFUVDUVMULZUWA
      UXTMVVGUWNUWAEUTZUXRUWASZUXSRZOEUIZUJZUWAUXTRZVVGUWNVVHVVKUVDUWNUVMUUQUUR
      UWNUVCUWOVGVCVVHVVGUCUDEUVRUWAUVLUVQVTUWAVEZWAZXJVVGVVJOEVVGVUKULVVIUVLUX
      RLSZSZUXSVUKVVIVVQTZVVGUCUDUXRUVRVVQEUWAUVNUXRTZUVLUVQVVPUVNUXRLVKZWKVVNU
      VLVVPVTWLZWMUVDUVMVUKVVQUXSRZUVCUUQUVMVUKULZVWBUURUVCVUKUVMVWBUVCVUKUVMVW
      BUVCVUKULZFUXSVVPUHZUVMVWBUVBVWENUXREUUSUXRTZUVBFUUTVVPUHZVWEVWFFUUTUVAVV
      PUUSUXRLVKXTVWFUUTUXSTVWGVWEWHUUSUXRGVKUUTUXSFVVPYCWGYDWPZFUXSUVLVVPWBXGZ
      YEYFYGYHYIVDYLOEUXSUWAYJZYKPYMVDUAUBFMUWAUWBUWPYNZURUUQUVCVVDUURUUQUVCULZ
      UWGLSZUWGHSZUWBUKZTZUEEUIVVDVWLVWPUEEVWLUWGERZULZVWPUWFUXHVWMSZUXHVWOSZTZ
      UFFUIZVWRVWMFUTZVWOFUTZVWPUWFVXBULWHUVCVWQVXCUUQUVCVWQULFUWGGSZVWMUHZVXCU
      VBVXFNUWGEUUSUWGTZUVBFUUTVWMUHZVXFVXGFUUTUVAVWMUUSUWGLVKXTVXGUUTVXETVXHVX
      FWHUUSUWGGVKUUTVXEFVWMYCWGYDWPFVXEVWMVAWGWIVWRVWNMUTZUWMUWBYOMYPZVXDVWRVX
      IUYNUYOUWGUYMSZTZULZABUPZMUTABMVXKVXNUWGUYMVTVXNVEZWAVWRMVWNVXNUUQVWQVWNV
      XNTZUVCVWQUUQVXPUUQVWQVXNUNRZVXPUUQVUIVXQVULABMVXKUNUOWGDCUWGVUQVXNEUNHVU
      MUWGTZVUPVXMABVXRVUOVXLUYNVXRVUNVXKUYOVUMUWGUYMVKVLVMVNQVQVOVPVSZYQXIZUUQ
      UWMUVCVWQUWQXFZVWRVVAVXJVWRVVFVVAVWRVVEUAFVWRUVMULZUWAUXTMVYBVVLVVMVYBUWN
      VVHVVKUUQUWNUVCVWQUVMUWOYRVVHVYBVVOXJVYBVVJOEVYBVUKULVVIVVQUXSVUKVVRVYBVW
      AWMVWRUVMVUKVWBVWLVWCVWBVWQUVCVWCVWBUUQUVCUVMVUKVWBUVCVUKUVMVWBVWIYSYEWIV
      SYHYIVDYLVWJYKPYMVDVWKURFMUWBYTWGMFVWNUWBUUAXOUFFFVWMVWOVHVIVWRFVJVWRVXAU
      FFVWRUXHFRZULZVWTUXHUWBSZVWNSZVWSVYDVWNWQZUWBWQZUXHUWBWRZRZVWTVYFTVYDVXIV
      YGVWRVXIVYCVXTVCMVWNWTWGVYDUWMVYHUUQUWMUVCVWQVYCUWQYRFUWBWTWGVWRVYJVYCVWR
      VYIFUXHVWRUWMVYIFTVYAFUWBUUBWGXMXNUXHVWNUWBYAXOVYDVYFUVOUVPUXHUVQSZTZULZU
      CUDUPZVWNSVYNVXNSZVWSVYDVYEVYNVWNVWLVYCVYEVYNTZVWQUUQVYCVYPUVCVYCUUQVYPUU
      QVYCVYNUNRZVYPUCUDEVYKIUOZUAUBUXHUWAVYNFUNUWBUVLUXHTZUVTVYMUCUDVYSUVSVYLU
      VOVYSUVRVYKUVPUVLUXHUVQVKVLVMVNUWPVQVOVPVSVSUUCVYDVYNVWNVXNVWRVXPVYCVXSVC
      WKVYDVYOUWGVYNSZVWSVWLVYCVYOVYTTZVWQVWLVYCULZVYNMRWUAWUBVYNUXTMWUBVYQVYNE
      UTZUXRVYNSZUXSRZOEUIZUJVYNUXTRWUBVYQWUCWUFUUQVYQUVCVYCVYRXFWUCWUBUCUDEVYK
      VYNUXHUVQVTVYNVEZWAXJUVCVYCWUFUUQUVCVYCULZWUEOEWUHVUKULWUDUXHVVPSZUXSVUKW
      UDWUITWUHUCUDUXRVYKWUIEVYNVVSUXHUVQVVPVVTWKWUGUXHVVPVTWLWMUVCVUKVYCWUIUXS
      RZVWDVWEVYCWUJVWHFUXSUXHVVPWBXGYSYIVDWIYLOEUXSVYNYJYKPYMABVYNVXKVYTMVXNUW
      GUYMVYNXPVXOUWGVYNVTWLWGVSVWQVYTVWSTVWLVYCUCUDUWGVYKVWSEVYNUVNUWGTUXHUVQV
      WMUVNUWGLVKWKWUGUXHVWMVTWLUUDUUEXQXSVDXRVDVWPVVCUENEUWGUUSTZVWMUVAVWOVVBU
      WGUUSLVKWUKVWNUVGUWBUWGUUSHVKWNWOUUFURUUGUUHUWCUVFVVAUVJVVDFMUVEUWBUUIUWC
      UVIVVCNEUWCUVHVVBUVAUVEUWBUVGUUNVLUUJUUKUULUUMUUOUUP $.
      $( [2-Sep-2009] $)
  $}

  $( Equality of equivalence classes.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  erthdmg $p |- ( ( B e. C /\ Er R /\ A e. dom R )
                                -> ( [ A ] R = [ B ] R <-> A R B ) ) $=
    ( wcel wer cdm cec wceq wbr wb cvv wi elisset c0 cif cid bibi12d eceq1 ereq
    eceq2 eqeq2d breq2 imbi2d dmeq eleq2d eqeq12d breq imbi12d 0ex ider elimhyp
    elimel erthdm dedth2h sylan 3impia ) BCEZDFZADGZEZADHZBDHZIZABDJZKZURBLEZUS
    VAVFMZBCNVGUSVHVAVBVGBOPZDHZIZAVIDJZKZMAUSDQPZGZEZAVNHZVIVNHZIZAVIVNJZKZMBD
    OQBVIIZVFVMVAWBVDVKVEVLWBVCVJVBBVIDUAUBBVIADUCRUDDVNIZVAVPVMWAWCUTVOADVNUEU
    FWCVKVSVLVTWCVBVQVJVRDVNASDVNVISUGAVIDVNUHRUIAVIVNBOLUJUMUSVNFQFDQDVNTQVNTU
    KULUNUOUPUQ $.
    $( [2-Sep-2009] $)

  ${
    $d R r $.  $d B r $.  $d A r $.
    $( Membership of an equivalence class in a quotient set.  (Contributed by
       Jeff Madsen, 10-Jun-2010.) $)
    ecelqsg $p |- ( ( R e. C /\ B e. A ) -> [ B ] R e. ( A /. R ) ) $=
      ( vr wcel cec cv wi wceq eceq1 qseq2 eleq12d imbi2d visset ecelqsi vtoclg
      cqs imp ) DCFBAFZBDGZADRZFZTBEHZGZAUDRZFZITUCIEDCUDDJZUGUCTUHUEUAUFUBUDDB
      KUDDALMNABUDEOPQS $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d A p q x $.  $d B p q y $.  $d R p q x $.  $d S p q y $.  $d x y z $.
    eroprlem.1 $e |- J = ( A /. R ) $.
    eroprlem.2 $e |- K = ( B /. S ) $.
    eroprlem.3 $e |- G = { <. <. x , y >. , z >. | E. p e. A E. q e. B
                ( ( x = [ p ] R /\ y = [ q ] S ) /\ z = [ ( p F q ) ] T ) } $.
    $( Lemma for ~ eroprv and ~ eroprf . $)
    eroprlem $p |- G = { <. <. x , y >. , z >. | ( ( x e. J /\ y e. K ) /\
                          E. p e. A E. q e. B ( ( x = [ p ] R /\ y = [ q ] S )
                                          /\ z = [ ( p F q ) ] T ) ) } $=
      ( cv wa wrex cec wceq co copab2 wcel simpl reximi cqs eleq2i df-qs abeq2i
      bitri anbi12i reeanv bitr4i sylibr pm4.71ri oprabbii eqtri ) JARZNRZFUAUB
      ZBRZMRZGUAUBZSZCRVAVDIUCHUAUBZSZMETZNDTZABCUDUTKUEZVCLUEZSZVJSZABCUDQVJVN
      ABCVJVMVJVFMETZNDTZVMVIVONDVHVFMEVFVGUFUGUGVMVBNDTZVEMETZSVPVKVQVLVRVKUTD
      FUHZUEVQKVSUTOUIVQAVSNADFUJUKULVLVCEGUHZUEVRLVTVCPUIVRBVTMBEGUJUKULUMVBVE
      NMDEUNUOUPUQURUS $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d A p q r s t u x w z $.  $d B p q r s t u y w z $.
    $d R p q r s t u x w z $.  $d S p q r s t u y w z $.
    $d T p q r s t u w z $.  $d F p q r s t u w z $.  $d ph p q r s t u w z $.
    $d J p q w z $.  $d K p q w z $.
    eropr.1 $e |- J = ( A /. R ) $.
    eropr.2 $e |- K = ( B /. S ) $.
    eropr.3 $e |- ( ph -> T e. Z ) $.
    eropr.4 $e |- ( ph -> Er R ) $.
    eropr.5 $e |- ( ph -> Er S ) $.
    eropr.6 $e |- ( ph -> Er T ) $.
    eropr.7 $e |- ( ph -> A C_ dom R ) $.
    eropr.8 $e |- ( ph -> B C_ dom S ) $.
    eropr.9 $e |- ( ph -> C C_ dom T ) $.
    eropr.10 $e |- ( ph -> F : ( A X. B ) --> C ) $.
    eropr.11 $e |- ( ( ph /\ ( ( r e. A /\ s e. A ) /\ ( t e. B /\ u e. B ) ) )
                        -> ( ( r R s /\ t S u ) -> ( r F t ) T ( s F u ) ) ) $.
    $( Lemma for ~ eroprv and ~ eroprf . $)
    eropreu $p |- ( ( ph /\ ( x e. J /\ y e. K ) ) -> E! z E. p e. A E. q e. B
                ( ( x = [ p ] R /\ y = [ q ] S ) /\ z = [ ( p F q ) ] T ) ) $=
      ( vw cv wcel wa cec wceq co wrex wex wi wal weu eleq2i df-qs abeq2i bitri
      cqs anbi12i biimpi adantl reeanv sylibr cvv adantr elex biantrud 2rexbidv
      ecexg mpbid 19.42v bicomi rexbii rexcom4 sylib eceq2 eqeq2d anbi1d opreq1
      3syl syl anbi12d rexbidv cbvrexv anbi2d opreq2 eqtr2 anim12i an4s wbr wer
      cdm visset a1i wss ssel2 sylan erthdmg syl111anc adantrr adantrl adantrlr
      wb adantrrr wf foprrn 3expb oprex syldan 3imtr4d sylan2 eqeq12 syl5ibrcom
      cxp imp expcom com23 com12 exp3a com34 rexlimdvv ex imp3a syl5bi alrimivv
      expdimp eqeq1 eu4 sylanbrc ) ABUMZNUNZCUMZOUNZUOZUOZYTTUMZJUPZUQZUUBSUMZK
      UPZUQZUOZDUMZUUFUUIMURZLUPZUQZUOZSHUSZTGUSZDUTZUUSUULULUMZUUOUQZUOZSHUSZT
      GUSZUOZUUMUVAUQZVAZULVBDVBUUSDVCUUEUULUUPDUTZUOZSHUSZTGUSZUUTUUEUULSHUSTG
      USZUVLUUEUUHTGUSZUUKSHUSZUOZUVMUUDUVPAUUDUVPUUAUVNUUCUVOUUAYTGJVHZUNUVNNU
      VQYTUAVDUVNBUVQTBGJVEVFVGUUCUUBHKVHZUNUVOOUVRUUBUBVDUVOCUVRSCHKVEVFVGVIVJ
      VKUUHUUKTSGHVLVMUUEUULUVJTSGHUUEUVIUULUUELPUNZUUOVNUNUVIAUVSUUDUCVOUUNPLV
      SDUUOVNVPWJVQVRVTUVLUURDUTZTGUSUUTUVKUVTTGUVKUUQDUTZSHUSUVTUVJUWASHUWAUVJ
      UULUUPDWAWBWCUUQSDHWDVGWCUURTDGWDVGWEUUEUVHDULAUVHUUDUVFYTRUMZJUPZUQZUUBF
      UMZKUPZUQZUOZUUMUWBUWEMURZLUPZUQZUOZFHUSZRGUSZYTQUMZJUPZUQZUUBEUMZKUPZUQZ
      UOZUVAUWOUWRMURZLUPZUQZUOZEHUSZQGUSZUOAUVGUUSUWNUVEUXGUUSUWDUUKUOZUUMUWBU
      UIMURZLUPZUQZUOZSHUSZRGUSUWNUURUXMTRGUUFUWBUQZUUQUXLSHUXNUULUXHUUPUXKUXNU
      UHUWDUUKUXNUUGUWCYTUUFUWBJWFWGWHUXNUUOUXJUUMUXNUUNUXIUQUUOUXJUQUUFUWBUUIM
      WIUUNUXILWFWKWGWLWMWNUXMUWMRGUXLUWLSFHUUIUWEUQZUXHUWHUXKUWKUXOUUKUWGUWDUX
      OUUJUWFUUBUUIUWEKWFWGWOUXOUXJUWJUUMUXOUXIUWIUQUXJUWJUQUUIUWEUWBMWPUXIUWIL
      WFWKWGWLWNWCVGUVEUWQUUKUOZUVAUWOUUIMURZLUPZUQZUOZSHUSZQGUSUXGUVDUYATQGUUF
      UWOUQZUVCUXTSHUYBUULUXPUVBUXSUYBUUHUWQUUKUYBUUGUWPYTUUFUWOJWFWGWHUYBUUOUX
      RUVAUYBUUNUXQUQUUOUXRUQUUFUWOUUIMWIUUNUXQLWFWKWGWLWMWNUYAUXFQGUXTUXESEHUU
      IUWRUQZUXPUXAUXSUXDUYCUUKUWTUWQUYCUUJUWSUUBUUIUWRKWFWGWOUYCUXRUXCUVAUYCUX
      QUXBUQUXRUXCUQUUIUWRUWOMWPUXQUXBLWFWKWGWLWNWCVGVIAUWNUXGUVGAUWLUXGUVGVAZR
      FGHAUWBGUNZUWEHUNZUOZUWLUYDVAAUYGUOZUXGUWLUVGUYHUXEUWLUVGVAQEGHUYHUWOGUNZ
      UWRHUNZUOZUWLUXEUVGAUYGUYKUWLUXEUVGVAVAZUYGUYKUOAUYLUYEUYIUYFUYJAUYLVAAUY
      EUYIUOZUYFUYJUOUOZUYLAUYNUOZUWLUXEUVGUWLUXEUOUYOUVGUWHUXAUWKUXDUYOUVGVAZU
      WHUXAUOZUWKUXDUOZUYPUYQUYOUYRUVGUYOUYQUYRUVGVAUYOUYQUOUVGUYRUWJUXCUQZUYQU
      YOUWCUWPUQZUWFUWSUQZUOZUYSUWDUWQUWGUWTVUBUWDUWQUOUYTUWGUWTUOVUAYTUWCUWPWQ
      UUBUWFUWSWQWRWSUYOVUBUYSUYOUWBUWOJWTZUWEUWRKWTZUOZUWIUXBLWTZVUBUYSUKAUYMU
      YFVUBVUEXMZUYJAUYEUYFVUGUYIUYHUYTVUCVUAVUDAUYEUYTVUCXMZUYFAUYEUOZUWOVNUNZ
      JXAZUWBJXBZUNZVUHVUJVUIQXCXDAVUKUYEUDVOAGVULXEUYEVUMUGGVULUWBXFXGUWBUWOVN
      JXHXIXJAUYFVUAVUDXMZUYEAUYFUOZUWRVNUNZKXAZUWEKXBZUNZVUNVUPVUOEXCXDAVUQUYF
      UEVOAHVURXEUYFVUSUHHVURUWEXFXGUWEUWRVNKXHXIXKWLXLXNAUYMUYFUYSVUFXMZUYJAUY
      EUYFVUTUYIAUYGUWIIUNZVUTAGHYDIMXOZUYGVVAUJVVBUYEUYFVVAUWBUWEIGHMXPXQXGAVV
      AUOZUXBVNUNZLXAZUWILXBZUNZVUTVVDVVCUWOUWRMXRXDAVVEVVAUFVOAIVVFXEVVAVVGUII
      VVFUWIXFXGUWIUXBVNLXHXIXSXLXNXTYEYAUUMUWJUVAUXCYBYCYFYGYEWSYHYIYFWSYHYPYJ
      YKYGYLYKYMYNVOYOUUSUVEDULUVGUUQUVCTSGHUVGUUPUVBUULUUMUVAUUOYQWOVRYRYS $.
      $( [10-Jun-2010] $)

    $d P x y z p q $.  $d Q x y z p q $.  $d A y $.  $d B x $.  $d R y $.
    $d S x $.  $d T x y $.  $d F x y $.  $d ph x y $.  $d J x y $.  $d K x y $.
    eropr.12 $e |- G = { <. <. x , y >. , z >. | E. p e. A E. q e. B
                ( ( x = [ p ] R /\ y = [ q ] S ) /\ z = [ ( p F q ) ] T ) } $.
    eropr.13 $e |- ( ph -> R e. X ) $.
    eropr.14 $e |- ( ph -> S e. Y ) $.
    $( The value of an operation defined on equivalence classes.  (Contributed
       by Jeff Madsen, 10-Jun-2010.) $)
    eroprv $p |- ( ( ph /\ P e. A /\ Q e. B )
                              -> ( [ P ] R G [ Q ] S ) = [ ( P F Q ) ] T ) $=
      ( wcel w3a cec co wceq cv wrex eqid pm3.2i eceq2 eqeq2d anbi1d opreq1 syl
      wa anbi12d anbi2d opreq2 rcla42ev mp3an3 3adant1 cvv wb simp1 cqs ecelqsg
      syl6eleqr sylan 3adant3 3adant2 ecexg 3ad2ant1 2rexbidv eropreu oprabvalg
      eqeq1 eroprlem syl13anc mpbird ) AJGUTZKHUTZVAZJLVBZKMVBZPVCJKOVCZNVBZVDZ
      XBUEVEZLVBZVDZXCUDVEZMVBZVDZVNZXEXGXJOVCZNVBZVDZVNZUDHVFUEGVFZWSWTXRAWSWT
      XBXBVDZXCXCVDZVNZXEXEVDZVNZXRYAYBXSXTXBVGXCVGVHXEVGVHXQYCXSXLVNZXEJXJOVCZ
      NVBZVDZVNUEUDJKGHXGJVDZXMYDXPYGYHXIXSXLYHXHXBXBXGJLVIVJVKYHXOYFXEYHXNYEVD
      XOYFVDXGJXJOVLXNYENVIVMVJVOXJKVDZYDYAYGYBYIXLXTXSYIXKXCXCXJKMVIVJVPYIYFXE
      XEYIYEXDVDYFXEVDXJKJOVQYEXDNVIVMVJVOVRVSVTXAAXBQUTZXCRUTZXEWAUTZXFXRWBAWS
      WTWCAWSYJWTALSUTZWSYJURYMWSVNXBGLWDQGJSLWEUFWFWGWHAWTYKWSAMTUTZWTYKUSYNWT
      VNXCHMWDRHKTMWEUGWFWGWIAWSYLWTANUAUTYLUHXDUANWJVMWKBVEZXHVDZCVEZXKVDZVNZD
      VEZXOVDZVNZUDHVFUEGVFXIYRVNZUUAVNZUDHVFUEGVFXMUUAVNZUDHVFUEGVFXRABCDXBXCX
      EWAQRPYOXBVDZUUBUUDUEUDGHUUFYSUUCUUAUUFYPXIYRYOXBXHWOVKVKWLYQXCVDZUUDUUEU
      EUDGHUUGUUCXMUUAUUGYRXLXIYQXCXKWOVPVKWLYTXEVDZUUEXQUEUDGHUUHUUAXPXMYTXEXO
      WOVPWLABCDEFGHILMNOQRUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPWMBCDGHLMNOPQRUDUEUF
      UGUQWPWNWQWR $.
      $( [10-Jun-2010] $)

    $d A m n $.  $d B m n $.  $d R m n $.  $d S m n $.  $d J j k $.
    $d K j k $.  $d L j k m n $.  $d G j k m n $.  $d ph j k m n $.
    $d m n p q x y z $.
    eropr.15 $e |- L = ( C /. T ) $.
    $( Functionality of an operation defined on equivalence classes.
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    eroprf $p |- ( ph -> G : ( J X. K ) --> L ) $=
      ( vj vk vm vn cxp wfn cv co wcel wral wf wa cec wceq wrex copab2 copab wi
      weu wal eropreu alrimivv fnoprabg syl eroprlem fneq1i df-xp fneq2i sylibr
      ex bitri cqs eleq2i visset elqs anbi12i reeanv eroprv 3expb sylan ecelqsg
      foprrn syldan syl6eleqr eqeltrd opreq12 eleq1d rexlimdvv syl5bi ralrimivv
      syl5ibrcom syl5bir ffnoprv sylanbrc ) ANOPVDZVEZUTVFZVAVFZNVGZQVHZVAPVIUT
      OVIXNQNVJABVFZOVHCVFZPVHVKZXTUDVFZJVLVMYAUCVFZKVLVMVKDVFYCYDMVGLVLVMVKUCH
      VNUDGVNZVKBCDVOZYBBCVPZVEZXOAYBYEDVRZVQZCVSBVSYHAYJBCAYBYIABCDEFGHIJKLMOP
      TUAUBUCUDUEUFUGUHUIUJUKULUMUNUOVTWIWAYBYEBCDWBWCXOYFXNVEYHXNNYFBCDGHJKLMN
      OPUCUDUEUFUPWDWEXNYGYFBCOPWFWGWJWHAXSUTVAOPXPOVHZXQPVHZVKXPVBVFZJVLZVMZVB
      GVNZXQVCVFZKVLZVMZVCHVNZVKZAXSYKYPYLYTYKXPGJWKZVHYPOUUBXPUEWLVBGXPJUTWMWN
      WJYLXQHKWKZVHYTPUUCXQUFWLVCHXQKVAWMWNWJWOUUAYOYSVKZVCHVNVBGVNAXSYOYSVBVCG
      HWPAUUDXSVBVCGHAYMGVHZYQHVHZVKZUUDXSVQAUUGVKZXSUUDYNYRNVGZQVHUUHUUIYMYQMV
      GZLVLZQAUUEUUFUUIUUKVMABCDEFGHIYMYQJKLMNOPRSTUAUBUCUDUEUFUGUHUIUJUKULUMUN
      UOUPUQURWQWRUUHUUKILWKZQAUUGUUJIVHZUUKUULVHZAGHVDIMVJZUUGUUMUNUUOUUEUUFUU
      MYMYQIGHMXAWRWSALTVHUUMUUNUGIUUJTLWTWSXBUSXCXDUUDXRUUIQXPYNXQYRNXEXFXJWIX
      GXKXHXIUTVAOPQNXLXM $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d A p q r s t u x y z $.  $d R p q r s t u x y z $.
    $d F p q r s t u x y z $.  $d ph p q r s t u x y z $.  $d J p q x y z $.
    $d P p q x y z $.  $d Q p q x y z $.
    eropr2.1 $e |- J = ( A /. R ) $.
    eropr2.2 $e |- G = { <. <. x , y >. , z >. | E. p e. A E. q e. A
                ( ( x = [ p ] R /\ y = [ q ] R ) /\ z = [ ( p F q ) ] R ) } $.
    eropr2.3 $e |- ( ph -> R e. X ) $.
    eropr2.4 $e |- ( ph -> Er R ) $.
    eropr2.5 $e |- ( ph -> A C_ dom R ) $.
    eropr2.6 $e |- ( ph -> F : ( A X. A ) --> A ) $.
    eropr2.7 $e |- ( ( ph /\ ( ( r e. A /\ s e. A ) /\ ( t e. A /\ u e. A ) ) )
                        -> ( ( r R s /\ t R u ) -> ( r F t ) R ( s F u ) ) ) $.
    $( The value of an operation defined on equivalence classes.  (Contributed
       by Jeff Madsen, 10-Jun-2010.) $)
    eroprv2 $p |- ( ( ph /\ P e. A /\ Q e. A )
                              -> ( [ P ] R G [ Q ] R ) = [ ( P F Q ) ] R ) $=
      ( eroprv ) ABCDEFGGGHIJJJKLMMNNNOPQRSSUAUBUBUBUCUCUCUDUETUAUAUF $.
      $( [10-Jun-2010] $)

    $( Functionality of an operation defined on equivalence classes.
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    eroprf2 $p |- ( ph -> G : ( J X. J ) --> J ) $=
      ( eroprf ) ABCDEFGGGHHHIJKKKLLLMNOPQQSTTTUAUAUAUBUCRSSQUD $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d ph z $.
    $( Existence of an existentially restricted class abstraction.  (Moved to
       ~ abrexex2g in main set.mm and may be deleted by mathbox owner, JM. --NM
       21-May-2012.) $)
    abrexex2gOLD $p |- ( ( A e. B /\ A. x e. A { y | ph } e. C )
                                  -> { y | E. x e. A ph } e. _V ) $=
      ( abrexex2g ) ABCDEFG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y $.
    abrexdom.1 $e |- ( y e. A -> E* x ph ) $.
    $( An indexed set is dominated by the indexing set.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    abrexdom $p |- ( A e. V -> { x | E. y e. A ph } ~<_ A ) $=
      ( wcel wrex cab cv wa copab crn cdom wex df-rex abbii wbr cvv wmo cdm wfn
      rnopab eqtr4i wss dmopabss ssexg mpan funopab wi moanimv mpbir mpgbir a1i
      wfun funfn sylib fnrndomg sylc ssdom2g mpi domtr syl11anc syl5eqbr ) DEGZ
      ACDHZBIZCJDGZAKZCBLZMZDNVGVICOZBIVKVFVLBACDPQVICBUCUDVEVKVJUAZNRZVMDNRZVK
      DNRVEVMSGZVJVMUBZVNVMDUEZVEVPACBDUFZVMDEUGUHVEVJUOZVQVTVEVTVIBTZCVICBUIWA
      VHABTUJFVHABUKULUMUNVJUPUQVMSVJURUSVEVRVOVSVMDEUTVAVKVMDVBVCVD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y $.  $d B x $.
    $( An indexed set is dominated by the indexing set.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    abrexdom2 $p |- ( A e. V -> { x | E. y e. A x = B } ~<_ A ) $=
      ( cv wceq wmo wcel moeq a1i abrexdom ) AFDGZABCEMAHBFCIADJKL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d v w x y z $.  $d A w x y z $.  $d ps x $.  $d ch x $.  $d th x $.
    $d ta x $.  $d ph v w y z $.
    findcard2.1OLD $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    findcard2.2OLD $e |- ( x = y -> ( ph <-> ch ) ) $.
    findcard2.3OLD $e |- ( x = ( y u. { z } ) -> ( ph <-> th ) ) $.
    findcard2.4OLD $e |- ( x = A -> ( ph <-> ta ) ) $.
    findcard2.5OLD $e |- ps $.
    findcard2.6OLD $e |- ( y e. Fin -> ( ch -> th ) ) $.
    $( Schema for induction on the cardinality of a finite set.  The inductive
       step shows that the result is true if one more element is added to the
       set.  The result is then proven to be true for all finite sets.  (Moved
       to ~ findcard2 in main set.mm and may be deleted by mathbox owner, JM.
       --NM 30-Nov-2012.) $)
    findcard2OLD $p |- ( A e. Fin -> ta ) $=
      ( findcard2 ) ABCDEFGHIJKLMNOP $.
      $( [8-Jul-2010] $)
  $}

  ${
    $d A s t u v w x y z $.  $d R s t u v w x y z $.
    fimax.1OLD $e |- R Or A $.
    $( A finite set has a maximum under a total order.  (Moved to ~ fimaxg in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Mar-2013.) $)
    fimaxOLD $p |- ( ( A e. Fin /\ A =/= (/) ) ->
                            E. x e. A A. y e. A ( x =/= y -> y R x ) ) $=
      ( wor cfn wcel c0 wne cv wbr wi wral wrex fimaxg mp3an1 ) CDFCGHCIJAKZBKZ
      JSRDLMBCNACOEABCDPQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R x y $.  $d A x y $.
    $( A finite set has a maximum under a total order.  (Moved to ~ fimaxg in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Mar-2013.) $)
    fimaxgOLD $p |- ( ( R Or A /\ A e. Fin /\ A =/= (/) ) ->
                            E. x e. A A. y e. A ( x =/= y -> y R x ) ) $=
      ( fimaxg ) ABCDE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R x y z $.  $d A x y z $.
    $( Lemma showing existence and closure of supremum of a finite set.  (Moved
       to ~ fisupg in main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Mar-2013.) $)
    fisupgOLD $p |- ( ( R Or A /\ A e. Fin /\ A =/= (/) ) -> E. x e. A ( A. y
        e. A
            -. x R y /\ A. y e. A ( y R x -> E. z e. A y R z ) ) ) $=
      ( fisupg ) ABCDEF $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z f $.  $d B x y z f $.  $d ph z f $.  $d ps z $.
    ac6gf.1 $e |- ( ps -> A. y ps ) $.
    ac6gf.2 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( Axiom of Choice.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    ac6gf $p |- ( ( A e. C /\ A. x e. A E. y e. B ph ) ->
                                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
      ( vz wrex wral wcel cv wsbc wf wa wex cvv ax-17 visset hbsbc1v cbvrex cfv
      sbceq1a ralbii wceq dfsbcq wb fvex wal a1i sbciegf ax-mp syl6bb ac6sg imp
      wi sylan2b ) ADFLZCEMEGNZADKOZPZKFLZCEMZEFHOZQBCEMRHSZVAVECEAVDDKFAKUAADV
      CKUBUCADVCUFUDUGVBVFVHVDBCKEFHGVCCOZVGUEZUHVDADVJPZBADVCVJUIVJTNZVKBUJVIV
      GUKABDVJTBBDULUSVLIUMJUNUOUPUQURUT $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A a g k $.  $d F a g k $.  $d B a g k $.
    $( Dependent choice.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    acdcg $p |- ( ( A e. B /\ A =/= (/) /\ F : A --> ( ~P A \ { (/) } ) ) ->
                              E. g ( g : NN --> A /\ A. k e. NN
                          ( g ` ( k + 1 ) ) e. ( F ` ( g ` k ) ) ) ) $=
      ( va wcel c0 wne cpw cdif wf cn cv cfv wa wex wi wceq feq3 csn caddc wral
      c1 co neeq1 feq2 wb pweq difeq1d syl anbi12d anbi1d exbidv imbi12d visset
      bitrd acdc vtoclg 3impib ) ABGAHIZAAJZHUAZKZELZMACNZLZDNZUDUBUEVFOVHVFOEO
      GDMUCZPZCQZFNZHIZVLVLJZVCKZELZPZMVLVFLZVIPZCQZRVAVEPZVKRFABVLASZVQWAVTVKW
      BVMVAVPVEVLAHUFWBVPAVOELZVEVLAVOEUGWBVOVDSWCVEUHWBVNVBVCVLAUIUJVOVDAETUKU
      QULWBVSVJCWBVRVGVIVLAMVFTUMUNUOVLCDEFUPURUSUT $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A a g k $.  $d B a g k $.  $d F a g k $.  $d C a g $.
    $( Dependent choice.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    acdc3g $p |- ( ( A e. B /\ F : A --> ( ~P A \ { (/) } ) /\ C e. A ) ->
                  E. g ( g : NN --> A /\ ( g ` 1 ) = C /\ A. k e. NN
                        ( g ` ( k + 1 ) ) e. ( F ` ( g ` k ) ) ) ) $=
      ( va wcel cpw cdif wf cn cv c1 cfv wceq w3a wex wa wi csn caddc wral feq2
      c0 co wb pweq difeq1d feq3 syl bitrd eleq2 anbi12d 3anbi1d exbidv imbi12d
      visset acdc3 vtoclg 3impib ) ABHAAIZUEUAZJZFKZCAHZLADMZKZNVGOCPZEMZNUBUFV
      GOVJVGOFOHELUCZQZDRZGMZVNIZVCJZFKZCVNHZSZLVNVGKZVIVKQZDRZTVEVFSZVMTGABVNA
      PZVSWCWBVMWDVQVEVRVFWDVQAVPFKZVEVNAVPFUDWDVPVDPWEVEUGWDVOVBVCVNAUHUIVPVDA
      FUJUKULVNACUMUNWDWAVLDWDVTVHVIVKVNALVGUJUOUPUQVNCDEFGURUSUTVA $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A a g k $.  $d B a g k $.  $d F a g k $.  $d C a g $.
    $( Dependent choice.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    acdc5g $p |- ( ( A e. B /\ F : ( NN X. A ) --> ( ~P A \ { (/) } )
            /\ C e. A ) -> E. g ( g : NN --> A /\ ( g ` 1 ) = C /\ A. k e. NN
                    ( g ` ( k + 1 ) ) e. ( ( k + 1 ) F ( g ` k ) ) ) ) $=
      ( va wcel cn cxp cpw cdif wf cv c1 cfv wceq co w3a wex c0 csn caddc wa wi
      wral xpeq2 feq2d pweq difeq1d feq3 syl bitrd eleq2 anbi12d 3anbi1d exbidv
      wb imbi12d visset acdc5 vtoclg 3impib ) ABHIAJZAKZUAUBZLZFMZCAHZIADNZMZOV
      JPCQZENZOUCRZVJPVNVMVJPFRHEIUFZSZDTZIGNZJZVRKZVFLZFMZCVRHZUDZIVRVJMZVLVOS
      ZDTZUEVHVIUDZVQUEGABVRAQZWDWHWGVQWIWBVHWCVIWIWBVDWAFMZVHWIVSVDWAFVRAIUGUH
      WIWAVGQWJVHURWIVTVEVFVRAUIUJWAVGVDFUKULUMVRACUNUOWIWFVPDWIWEVKVLVOVRAIVJU
      KUPUQUSVRCDEFGUTVAVBVC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y c z w v q $.  $d B x y c z w v q $.  $d ph c z w v q $.
    $( If for every element of an indexing set ` A ` there exists a
       corresponding element of another set ` B ` , then there exists a subset
       of ` B ` consisting only of those elements which are indexed by ` A ` .
       Used to avoid the Axiom of Choice in situations where only the range of
       the choice function is needed.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    indexa $p |- ( ( B e. M /\ A. x e. A E. y e. B ph ) -> E. c
          ( c C_ B /\ A. x e. A E. y e. c ph /\ A. y e. c E. x e. A ph ) ) $=
      ( vz vw vv vq wcel cv wsbc wrex wral ax-17 wa wceq hbsb3 crab cvv wss w3a
      wex rabexg ssrab2 a1i hbre1 wb sbceq1a eqcoms bicomd ancoms anim2i anasss
      rcla4ev sbbid rexbidv elrab sylancom hbsb hbrex hbrab cbvrexf sylib exp31
      sylibr rexlimd ralimia cbvrex syl6bb elrabf simprbi rgen 3jca hbeq rexeqf
      sseq1 ralbid raleqf 3anbi123d cla4egv imp syl2an ) EFLACHMZNZBIMZNZIDOZHE
      UAZUBLZWKEUCZACWKOZBDPZABDOZCWKPZUDZGMZEUCZACWSOZBDPZWPCWSPZUDZGUEZACEOZB
      DPZWJHEFUFXGWMWOWQWMXGWJHEUGUHXFWNBDBMZDLZAWNCEXICQACWKUIXICMZELZAWNXIXKR
      ZARZACJMZNZJWKOZWNXLAXJWKLZXPXMXKABWHNZIDOZRZXQAXLXTAXIXKXTXKAXIRZXTYAXSX
      KXIAXSXRAIXHDWHXHSAXRAXRUJXHWHABWHUKULUMZUQUNUOUNUPUNWJXSHXJEWFXJSZWIXRID
      YCWGABIYCBQYCAWGAWGUJXJWFACWFUKULUMURUSZUTVHXOAJXJWKXNXJSAXOAXOUJXJXNACXN
      UKULUMZUQVAXOAJCKWKKMZWKLJQWJCHKEWICIDWHDLZCQWGBICACHAHQTVBVCYFELZCQVDZAC
      JAJQZTYJYEVEVFVGVIVJWQXGWPCWKXQXKWPWJWPHKXJEYFXJLHQYHHQWPHQYCWJXSWPYDXRAI
      BDABIAIQZTYKYBVKVLVMVNVOUHVPWLWRXEXDWRGWKUBWSWKSZWTWMXBWOXCWQWSWKEVSYLXAW
      NBDBKKWSWKYFWSLZBQWJBHKEWIBIDYGBQWGBIWGIQTVCYHBQVDVQACKWSWKYMCQZYIVRVTWPC
      KWSWKYNYIWAWBWCWDWE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A c f x y $.  $d B c f x y $.  $d ph f c $.
    $( If for every element of an indexing set ` A ` there exists a
       corresponding element of another set ` B ` , then there exists a subset
       of ` B ` consisting only of those elements which are indexed by ` A ` ,
       and which is dominated by the set ` A ` .  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    indexdom $p |- ( ( A e. M /\ A. x e. A E. y e. B ph ) ->
            E. c ( ( c ~<_ A /\ c C_ B ) /\ ( A. x e. A E. y e. c ph
                                        /\ A. y e. c E. x e. A ph ) ) ) $=
      ( vf wcel wrex wral wa cv wex cdom wbr cvv adantr ax-17 anbi12d wsbc fvex
      wf cfv wss hbsbc1v sbceq1a crn wfn cdm fdm visset dmex syl6eqelr fnrndomg
      ac6gf ffn sylc frn hbra1 hban wfun eleq2d biimpar fvelrn syl11anc adantlr
      ffun ra4 imp adantll rcla4e ex ralrimi hbral wb fvelrnb syl adantl eqcoms
      wceq wi biimprcd syl6 reximdai sylbid rnex breq1 sseq1 rexeq raleq cla4ev
      ralbidv syl22anc exlimiv ) DFIACEJBDKLDEHMZUCZACBMZWPUDZUAZBDKZLZHNGMZDOP
      ZXCEUEZLZACXCJZBDKZABDJZCXCKZLZLZGNZAWTBCDEFHACWSWRWPUBUFZACWSUGZUPXBXMHX
      BWPUHZDOPZXPEUEZACXPJZBDKZXICXPKZXMWQXQXAWQDQIWPDUIZXQWQDWPUJZQDEWPUKZWPH
      ULZUMUNDEWPUQZDQWPUOURRWQXRXADEWPUSRXBXSBDWQXABWQBSWTBDUTVAZXBWRDIZXSXBYH
      LWSXPIZWTXSWQYHYIXAWQYHLWPVBZWRYCIZYIWQYJYHDEWPVHRWQYKYHWQYCDWRYDVCVDWRWP
      VEVFVGXAYHWTWQXAYHWTWTBDVIZVJVKAWTCWSXPXNXOVLVFVMVNXBXICXPWQXACWQCSWTCBDY
      HCSXNVOVAXBCMZXPIZWSYMWAZBDJZXIWQYNYPVPZXAWQYBYQYFBDYMWPVQVRRXBYOABDYGXBY
      HWTYOAWBXAYHWTWBWQYLVSYOAWTAWTVPYMWSXOVTWCWDWEWFVNXLXQXRLZXTYALZLGXPWPYEW
      GXCXPWAZXFYRXKYSYTXDXQXEXRXCXPDOWHXCXPEWITYTXHXTXJYAYTXGXSBDACXCXPWJWMXIC
      XCXPWKTTWLWNWOVR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A a b x y c z w m n p $.  $d B a b x y c z w m n p $.
    $d ph a b c z w m n p $.  $d M a b m n p z y x w c $.
    $( If for every element of a finite indexing set ` A ` there exists a
       corresponding element of another set ` B ` , then there exists a finite
       subset of ` B ` consisting only of those elements which are indexed by
       ` A ` .  Proven without the Axiom of Choice, unlike ~ indexdom .  (Moved
       to ~ indexfi in main set.mm and may be deleted by mathbox owner, JM.
       --NM 16-Apr-2012.) $)
    indexfiOLD $p |- ( ( A e. Fin /\ B e. M /\ A. x e. A E. y e. B ph ) ->
                E. c e. Fin ( c C_ B /\ A. x e. A E. y e. c ph /\
                                                A. y e. c E. x e. A ph ) ) $=
      ( indexfi ) ABCDEFGH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F c x y $.  $d A c x y $.  $d B c x y $.  $d M c x y $.
    $( Given a finite subset ` A ` of the range of a function, there exists a
       finite subset of the domain whose image is ` A ` .  (Moved to ~ fipreima
       in main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Apr-2012.) $)
    fipreimaOLD $p |- ( ( ( F Fn B /\ B e. M ) /\ ( A C_ ran F /\ A e. Fin ) )
                                -> E. c e. ( ~P B i^i Fin ) ( F " c ) = A ) $=
      ( fipreima ) ABCDEF $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R x y z $.  $d A x y z $.  $d B x y z $.
    $( A subset of a well founded set has an infimum.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    frinfm $p |- ( ( R Fr A /\ ( B e. C /\ B C_ A /\ B =/= (/) ) ) ->
                    E. x e. A ( A. y e. B -. x `' R y /\
                    A. y e. A ( y `' R x -> E. z e. B y `' R z ) ) ) $=
      ( wfr wcel wss c0 wa cv wbr wn wral wrex wi visset ex wne w3a fri ancom1s
      ccnv exp43 3imp2 ssel2 adantrr brcnv biimpi con3i ralimi ad2antll rcla4ev
      breq2 a1d ralrimiv ad2antrl jca32 reximdv2 adantl 3ad2antr2 mpd ) DGHZEFI
      ZEDJZEKUAZUBLBMZAMZGNZOZBEPZAEQZVJVIGUEZNZOZBEPZVIVJVONZVICMZVONZCEQZRZBD
      PZLZADQZVEVFVGVHVNVEVFVGVHVNVFVEVGVHLVNABDEFGUCUDUFUGVEVFVGVNWFRZVHVGWGVE
      VGVMWEAEDVGVJEIZVMLZVJDIZWELVGWILWJVRWDVGWHWJVMEDVJUHUIVMVRVGWHVLVQBEVPVK
      VPVKVJVIGASBSUJUKULUMUNWHWDVGVMWHWCBDWHWCVIDIWHVSWBWAVSCVJEVTVJVIVOUPUOTU
      QURUSUTTVAVBVCVD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d R x y z $.
    $( A non-empty subset of a well ordered set has a lower bound.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    welb $p |- ( ( R We A /\ ( B e. C /\ B C_ A /\ B =/= (/) ) ) ->
              ( `' R Or B /\ E. x e. B ( A. y e. B -. x `' R y /\
                        A. y e. B ( y `' R x -> E. z e. B y `' R z ) ) ) ) $=
      ( wwe wcel wss c0 w3a wa wor cv wbr wral wrex 3ad2antr2 id wne ccnv wn wi
      wess impcom weso cnvso biimpi 3syl wfr wefr syl ssid a1i 3anim123i adantl
      frinfm syl11anc jca ) DGHZEFIZEDJZEKUAZLZMZEGUBZNZAOZBOZVGPUCBEQVJVIVGPVJ
      COVGPCERUDBEQMAERZVAVBVCVHVDVAVCMZEGHZEGNZVHVCVAVMEDGUEUFZEGUGVNVHEGUHUIU
      JSVFEGUKZVBEEJZVDLZVKVAVBVCVPVDVLVMVPVOEGULUMSVEVRVAVBVBVCVQVDVDVBTVQVCEU
      NUOVDTUPUQABCEEFGURUSUT $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d R x y z $.
    $( Equality theorem for supremum.  (Moved to ~ supeq2 in main set.mm and
       may be deleted by mathbox owner, JM. --NM 24-Sep-2013.) $)
    supeq2OLD $p |- ( B = C -> sup ( A , B , R ) = sup ( A , C , R ) ) $=
      ( supeq2 ) ABCDE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d R x y z $.
    $( Existence of supremum.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    supex2g $p |- ( A e. C -> sup ( B , A , R ) e. _V ) $=
      ( vx vy vz wcel csup cv wbr wn wral wrex wi wa crab cuni cvv df-sup syl
      rabexg uniexg syl5eqel ) ACHZBADIEJZFJZDKLFBMUGUFDKUGGJDKGBNOFAMPZEAQZRZS
      EFGBADTUEUISHUJSHUHEACUBUISUCUAUD $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d R x y z $.
    $( Closure of supremum.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    supclt $p |- ( ( R Or A /\ E. x e. A ( A. y e. B -. x R y /\ A. y e. A
            ( y R x -> E. z e. B y R z ) ) ) -> sup ( B , A , R ) e. A ) $=
      ( wor cv wbr wn wral wrex wi wa csup wcel c0 cif wceq soeq2 anbi2d supeq2
      raleq rexeqbi1dv id eleq12d imbi12d so0 elimhyp supcl dedth imp ) DFGZAHZ
      BHZFIJBEKZUOUNFIUOCHFICELMZBDKZNZADLZEDFOZDPZUMUTVBMUPUQBUMDQRZKZNZAVCLZE
      VCFOZVCPZMDQDVCSZUTVFVBVHUSVEADVCVIURVDUPUQBDVCUCUAUDVIVAVGDVCEDVCFUBVIUE
      UFUGABCVCEFUMVCFGQFGDQDVCFTQVCFTFUHUIUJUKUL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d R x y z $.
    $( Upper bound property of supremum.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    supubt $p |- ( ( R Or A /\ E. x e. A ( A. y e. B -. x R y /\
                        A. y e. A ( y R x -> E. z e. B y R z ) ) ) ->
                                ( C e. B -> -. sup ( B , A , R ) R C ) ) $=
      ( wor cv wbr wn wral wrex wi wa wcel csup c0 cif soeq2 wceq anbi2d supeq2
      raleq rexeqbi1dv breq1d notbid imbi2d imbi12d so0 elimhyp supub dedth imp
      ) DGHZAIZBIZGJKBELZUQUPGJUQCIGJCEMNZBDLZOZADMZFEPZEDGQZFGJZKZNZUOVBVGNURU
      SBUODRSZLZOZAVHMZVCEVHGQZFGJZKZNZNDRDVHUAZVBVKVGVOVAVJADVHVPUTVIURUSBDVHU
      DUBUEVPVFVNVCVPVEVMVPVDVLFGEDVHGUCUFUGUHUIABCVHEFGUOVHGHRGHDRDVHGTRVHGTGU
      JUKULUMUN $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.
    $( A member of a non-empty bounded set of reals is greater than or equal to
       the set's lower bound.  (Contributed by Jeff Madsen, 2-Feb-2011.) $)
    infmrlb $p |- ( ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y )
                            /\ B e. A ) -> sup ( A , RR , `' < ) <_ B ) $=
      ( vz cr cv cle wbr wral wrex wcel wa clt wn wi visset brcnv ralbii rexbii
      wss c0 wne w3a ccnv csup infm3 notbii imbi12i anbi12i wor ltso cnvso mpbi
      supub sylbir syl imp ssel2 3ad2antl1 infmrcl lenlt sylan syldan cvv supex
      wb a1i brcnvg notbid bitr4d mpbird ) CFUAZCUBUCZAGZBGZHIBCJAFKZUDZDCLZMZC
      FNUEZUFZDHIZWBDWAIZOZVRVSWEVRVPVONIZOZBCJZVOVPNIZEGZVPNIZECKZPZBFJZMZAFKZ
      VSWEPZABECUGWPVOVPWAIZOZBCJZVPVOWAIZVPWJWAIZECKZPZBFJZMZAFKWQXFWOAFWTWHXE
      WNWSWGBCWRWFVOVPNAQZBQZRUHSXDWMBFXAWIXCWLVPVONXHXGRXBWKECVPWJNXHEQRTUISUJ
      TABEFCDWAFNUKFWAUKULFNUMUNZUOUPUQURVTWCDWBNIZOZWEVRVSDFLZWCXKVGZVMVNVSXLV
      QCFDUSUTVRWBFLXLXMABCVAWBDVBVCVDVRWBVELZVSWEXKVGXNVRFCWAXIVFVHXNVSMWDXJWB
      DVECNVIVJVCVKVL $.
      $( [2-Feb-2011] $)
  $}

  ${
    $d A x y z $.  $d B z $.
    $( The infimum of a non-empty bounded set of reals is greater than or equal
       to a lower bound.  (Contributed by Jeff Madsen, 2-Feb-2011.) $)
    infmrgelb $p |- ( ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y )
        /\ ( B e. RR /\ A. z e. A B <_ z ) ) -> B <_ sup ( A , RR , `' < ) ) $=
      ( cr cv cle wbr wral wrex wcel wa clt wn wb lenlt wi visset brcnv wss wne
      w3a ccnv csup ssel2 brcnvg notbid bitr4d sylan2 anassrs ralbidva pm5.32da
      c0 ancom1s 3ad2ant1 infm3 notbii ralbii rexbii imbi12i anbi12i sylibr wor
      ltso cnvso mpbi supnub syl imp infmrcl ancoms supex adantl adantrr mpbird
      cvv mpan2 ex sylbid ) DFUAZDUNUBZAGZBGZHIBDJAFKZUCZEFLZECGZHIZCDJZMZEDFNU
      DZUEZHIZWFWKWGEWHWLIZOZCDJZMZWNWAWBWKWRPWEWAWGWJWQWAWGMWIWPCDWGWAWHDLZWIW
      PPZWGWAWSWTWAWSMWGWHFLZWTDFWHUFWGXAMZWIWHENIZOWPEWHQXBWOXCEWHFFNUGUHUIUJU
      KUOULUMUPWFWRWNWFWRMWNEWMWLIZOZWFWRXEWFWCWDWLIZOZBDJZWDWCWLIZWDWHWLIZCDKZ
      RZBFJZMZAFKZWRXERWFWDWCNIZOZBDJZWCWDNIZWHWDNIZCDKZRZBFJZMZAFKXOABCDUQXNYD
      AFXHXRXMYCXGXQBDXFXPWCWDNASZBSZTURUSXLYBBFXIXSXKYAWDWCNYFYETXJXTCDWDWHNYF
      CSTUTVAUSVBUTVCABCFDEWLFNVDFWLVDVEFNVFVGZVHVIVJWFWGWNXEPWQWFWGMZWNWMENIZO
      ZXEWGWFWNYJPZWFWGWMFLYKABDVKEWMQUJVLYHXDYIWGXDYIPZWFWGWMVQLYLFDWLYGVMEWMF
      VQNUGVRVNUHUIVOVPVSVTVJ $.
      $( [2-Feb-2011] $)
  $}

  ${
    $d A x y z $.  $d R x y z $.  $d B x y z $.
    $( A supremum is unique.  Closed version of ~ supeu .  (Moved to ~ supeut
       in main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Mar-2013.) $)
    supeutOLD $p |- ( ( R Or A /\ E. x e. A ( A. y e. B -. x R y /\ A. y e. A
        ( y R x -> E. z e. B y R z ) ) ) -> E! x e. A ( A. y e. B -. x R y /\
                          A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( supeut ) ABCDEFG $.
      $( [9-May-2011] $)

    $( A nonempty finite set contains its supremum.  (Moved to ~ fisup2g in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Mar-2013.) $)
    fisup2gOLD $p |- ( ( R Or A /\ ( B e. Fin /\ B =/= (/) /\ B C_ A ) )
                                    -> sup ( B , A , R ) e. B ) $=
      ( fisup2g ) ABCD $.
      $( [9-May-2011] $)
  $}

  ${
    $d R x y $.  $d A x y $.
    $( A finite set has a maximum under a total order.  (Moved to ~ fimax2g in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Mar-2013.) $)
    fimax2gOLD $p |- ( ( R Or A /\ A e. Fin /\ A =/= (/) ) ->
                                        E. x e. A A. y e. A -. x R y ) $=
      ( fimax2g ) ABCDE $.
      $( [18-Jun-2010] $)
  $}

  ${
    $d R x y z $.  $d A x y z $.
    $( A total order on a finite set is a well order.  (Moved to ~ wofi in main
       set.mm and may be deleted by mathbox owner, JM. --NM 16-Mar-2013.) $)
    wofiOLD $p |- ( ( R Or A /\ A e. Fin ) -> R We A ) $=
      ( wofi ) ABC $.
      $( [18-Jun-2010] $)
  $}

  ${
    $d R u v w x y z $.  $d A x y z $.
    $( A partial order is founded on a finite set.  (Moved to ~ frfi in main
       set.mm and may be deleted by mathbox owner, JM. --NM 16-Mar-2013.) $)
    frfiOLD $p |- ( ( R Po A /\ A e. Fin ) -> R Fr A ) $=
      ( frfi ) ABC $.
      $( [18-Jun-2010] $)
  $}

  ${
    $d R v w x y z $.  $d S v w z $.  $d X v w y z $.  $d Y x z $.
    $d A v w x z $.  $d B v w x z $.
    pofun.1OLD $e |- S = { <. x , y >. | X R Y } $.
    pofun.2OLD $e |- ( x = y -> X = Y ) $.
    $( A function preserves a partial order relation.  (Moved to ~ pofun in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Mar-2013.) $)
    pofunOLD $p |- ( ( R Po B /\ A. x e. A X e. B ) -> S Po A ) $=
      ( pofun ) ABCDEFGHIJK $.
      $( [18-Jun-2010] $)
  $}

  ${
    $d A x y z $.  $d R x y z $.  $d ph y z $.  $d ps x $.
    frminexOLD.1 $e |- A e. _V $.
    frminexOLD.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( If an element of a founded set satisfies a property ` ph ` , then there
       is a minimal element that satisfies ` ph ` .  (Moved to ~ frminex in
       main set.mm and may be deleted by mathbox owner, JM. --NM
       16-Mar-2013.) $)
    frminexOLD $p |- ( R Fr A -> ( E. x e. A ph ->
                      E. x e. A ( ph /\ A. y e. A ( ps -> -. y R x ) ) ) ) $=
      ( frminex ) ABCDEFGHI $.
      $( [18-Jun-2010] $)
  $}

