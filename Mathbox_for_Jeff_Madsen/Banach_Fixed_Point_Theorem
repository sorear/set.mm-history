$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Banach Fixed Point Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F a b c k w x y z $.  $d X a b c k w x y z $.  $d Z x y z $.
    $d D k u v $.  $d N a b c u v x y z $.  $d Y a b c x y z $.  $d W x y z $.
    $d f j k w x z $.  $d G k u v x y z $.  $d M k u v w x y z $.
    $d K u v k w x y z $.
    bfp.1 $e |- X = dom dom M $.

    ${
      bfplem.2 $e |- M e. CMet $.
      bfplem.3 $e |- F : X --> X $.
      bfplem.4 $e |- Y e. X $.
      bfplem.5 $e |- G = ( { <. <. a , b >. , c >. | ( ( a e. X /\ b e. X )
                                /\ c = ( F ` a ) ) } seq1 ( NN X. { Y } ) ) $.
      $( Lemma for ~ bfp .  We construct a sequence ` G ` by repeatedly
         applying ` F ` to a starting value ` Y ` . $)
      bfplem1 $p |- G : NN --> X $=
        ( cn wf cv wcel wa cxp cdm cfv wceq copab2 csn co fconst6 eqid ffvelrni
        cseq1 adantr foprab cvv cms elexi dmex eqeltri oprabex2 nnex snex seq1f
        xpex mp2an feq1i mpbir ) NDBONDFPZDQZGPDQZRHPVEAUAZUBRFGHUCZNEUDZSZUIUE
        ZOZNDVKODDSDVIOVMNEDLUFFGHDDVHDVIVIUGZVFVHDQVGDDVEAKUHUJUKDVIVKFGHDDVHV
        IDCTZTULIVOCCUMJUNUOUOUPZVPVNUQNVJUREUSVAUTVBNDBVLMVCVD $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  The first element in the sequence is ` Y ` . $)
      bfplem2 $p |- ( G ` 1 ) = Y $=
        ( c1 cfv cv wcel wa wceq cn copab2 csn cxp cseq1 co fveq1i cdm cvv dmex
        cms elexi eqeltri eqid oprabex2 nnex snex xpex seq11 1nn fvconst2 ax-mp
        3eqtri ) NBONFPZDQGPDQRHPVCAOZSRFGHUAZTEUBZUCZUDUEZONVGOZENBVHMUFVEVGFG
        HDDVDVEDCUGZUGUHIVJCCUJJUKUIUIULZVKVEUMUNTVFUOEUPUQURNTQVIESUSTENEDLUKU
        TVAVB $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  The next element in the sequence is obtained by
         applying ` F ` to the previous element. $)
      bfplem3 $p |- ( N e. NN -> ( G ` ( N + 1 ) ) = ( F ` ( G ` N ) ) ) $=
        ( vx vy cn wcel cfv wceq vz c1 caddc co csn cxp cv copab2 cseq1 cdm cvv
        wa cms elexi dmex eqeltri eqid oprabex2 nnex snex seq1p1 fveq1i opreq1d
        xpex a1i 3eqtr4d bfplem1 ffvelrni peano2nn fvconst2 syl6eqel fvex fveq2
        syl eqidd eleq1 bi2anan9 eqeqan12rd cbvoprab12v eqeq1 anbi2d cbvoprab3v
        anbi12d eqtri oprabval2 syl2anc eqtrd ) DQRZDUBUCUDZBSZDBSZWIQFUEZUFZSZ
        GUGZERZHUGZERZULZIUGZWOASZTZULZGHIUHZUDZWKASZWHWIXDWMUIUDZSZDXGSZWNXDUD
        WJXEDXDWMGHIEEXAXDECUJZUJUKJXJCCUMKUNUOUOUPZXKXDUQURQWLUSFUTVDVAWJXHTWH
        WIBXGNVBVEWHWKXIWNXDWKXITWHDBXGNVBVEVCVFWHWKERWNERXEXFTQEDBABCEFGHIJKLM
        NVGVHWHWNFEWHWIQRWNFTDVIQFWIFEMUNVJVNMVKOPUAWKWNEEOUGZASZXFXDXFWKAVLXLW
        KAVMPUGZWNTXFVOXDXLERZXNERZULZWTXMTZULZOPIUHXQUAUGZXMTZULZOPUAUHXCXSGHI
        OPWOXLTZWQXNTZULWSXQXBXRYCWPXOYDWRXPWOXLEVPWQXNEVPVQYDYCWTWTXAXMYDWTVOW
        OXLAVMVRWCVSXSYBOPIUAWTXTTXRYAXQWTXTXMVTWAWBWDWEWFWG $.
        $( [2-Sep-2009] $)

      bfplem.6 $e |- D = ( Y M ( F ` Y ) ) $.
      $( Lemma for ~ bfp .  Define the initial distance ` D ` . $)
      bfplem4 $p |- ( D e. RR /\ 0 <_ D ) $=
        ( cr wcel cc0 cle wbr cfv co cme cmsmeti wf ffvelrn mp2an metcl eqeltri
        mp3an metge0 breqtrri pm3.2i ) APQRASTAFFBUAZDUBZPODUCQZFEQZUNEQZUOPQDK
        UDZMEEBUEUQURLMEEFBUFUGZFUNDEJUHUJUIRUOASUPUQURRUOSTUSMUTFUNDEJUKUJOULU
        M $.
        $( [2-Sep-2009] $)

      bfplem.7 $e |- K e. RR+ $.
      bfplem.8 $e |- K < 1 $.
      bfplem.9 $e |- A. x e. X A. y e. X
                            ( ( F ` x ) M ( F ` y ) ) <_ ( K x. ( x M y ) ) $.
      $( Lemma for ~ bfp .  Recursively bound the distance between consecutive
         elements of the sequence ` G ` based on the contraction property. $)
      bfplem5 $p |- ( N e. NN -> ( ( G ` ( N + 1 ) ) M ( G ` ( ( N + 1 ) + 1 )
                        ) ) <_ ( K x. ( ( G ` N ) M ( G ` ( N + 1 ) ) ) ) ) $=
        ( cn wcel caddc cfv cmul cle bfplem3 wceq peano2nn syl opreq12d bfplem1
        c1 co wbr ffvelrni wa wral fveq2 opreq1d opreq1 opreq2d breq12d rcla42v
        cv opreq2 mpi syl2anc eqbrtrd ) HUCUDZHUOUEUPZEUFZVMUOUEUPEUFZGUPHEUFZD
        UFZVNDUFZGUPZFVPVNGUPZUGUPZUHVLVNVQVOVRGDEGHIJKLMNOPQRUIVLVMUCUDZVOVRUJ
        HUKZDEGVMIJKLMNOPQRUIULUMVLVPIUDZVNIUDZVSWAUHUQZUCIHEDEGIJKLMNOPQRUNZUR
        VLWBWEWCUCIVMEWGURULWDWEUSAVGZDUFZBVGZDUFZGUPZFWHWJGUPZUGUPZUHUQZBIUTAI
        UTWFUBWOWFVQWKGUPZFVPWJGUPZUGUPZUHUQABVPVNIIWHVPUJZWLWPWNWRUHWSWIVQWKGW
        HVPDVAVBWSWMWQFUGWHVPWJGVCVDVEWJVNUJZWPVSWRWAUHWTWKVRVQGWJVNDVAVDWTWQVT
        FUGWJVNVPGVHVDVEVFVIVJVK $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  Bound the distance between consecutive elements of
         the sequence ` G ` by applying ~ bfplem5 inductively. $)
      bfplem6 $p |- ( N e. NN -> ( ( G ` N ) M ( G ` ( N + 1 ) ) )
                                      <_ ( D x. ( K ^ ( N - 1 ) ) ) ) $=
        ( vu vv cv cfv c1 caddc cmin cexp cmul cle wbr wceq fveq2 opreq1 fveq2d
        co opreq12d opreq2d breq12d bfplem2 wcel 1nn bfplem3 ax-mp fveq2i eqtri
        cn opreq12i eqtr4i cr cc0 bfplem4 simpli ax-1cn subidi opreq2i crp rpcn
        cc exp0 recni mulid1i eqtr2i eqle mp2an eqbrtri cme cmsmeti a1i bfplem1
        wa ffvelrni peano2nn syl metcl syl3anc cn0 rpre nnm1nn0 reexpcl sylancr
        wi remulcl rpge0 jca w3a lemul2a nncn addsub expp1 recnd mulcom sylancl
        ex 3eqtrd mul12 eqtrd breq2d sylibrd bfplem5 letr mpand syld nnind ) UC
        UEZEUFZYGUGUHURZEUFZGURZCFYGUGUIURZUJURZUKURZULUMUGEUFZUGUGUHURZEUFZGUR
        ZCFUGUGUIURZUJURZUKURZULUMUDUEZEUFZUUBUGUHURZEUFZGURZCFUUBUGUIURZUJURZU
        KURZULUMZUUEUUDUGUHURZEUFZGURZCFUUDUGUIURZUJURZUKURZULUMZHEUFZHUGUHURZE
        UFZGURZCFHUGUIURZUJURZUKURZULUMUCUDHYGUGUNZYKYRYNUUAULUVEYHYOYJYQGYGUGE
        UOUVEYIYPEYGUGUGUHUPUQUSUVEYMYTCUKUVEYLYSFUJYGUGUGUIUPUTUTVAYGUUBUNZYKU
        UFYNUUIULUVFYHUUCYJUUEGYGUUBEUOUVFYIUUDEYGUUBUGUHUPUQUSUVFYMUUHCUKUVFYL
        UUGFUJYGUUBUGUIUPUTUTVAYGUUDUNZYKUUMYNUUPULUVGYHUUEYJUULGYGUUDEUOUVGYIU
        UKEYGUUDUGUHUPUQUSUVGYMUUOCUKUVGYLUUNFUJYGUUDUGUIUPUTUTVAYGHUNZYKUVAYNU
        VDULUVHYHUURYJUUTGYGHEUOUVHYIUUSEYGHUGUHUPUQUSUVHYMUVCCUKUVHYLUVBFUJYGH
        UGUIUPUTUTVAYRCUUAULYRJJDUFZGURCYOJYQUVIGDEGIJKLMNOPQRVBZYQYODUFZUVIUGV
        IVCYQUVKUNVDDEGUGIJKLMNOPQRVEVFYOJDUVJVGVHVJSVKCVLVCZCUUAUNCUUAULUMUVLV
        MCULUMCDEGIJKLMNOPQRSVNVOZUUACUGUKURCYTUGCUKYTFVMUJURZUGYSVMFUJUGVPVQVR
        FWAVCZUVNUGUNFVSVCZUVOTFVTVFZFWBVFVHVRCCUVMWCZWDWECUUAWFWGWHUUBVIVCZUUJ
        FUUFUKURZUUPULUMZUUQUVSUUJUVTFUUIUKURZULUMZUWAUVSUUFVLVCZUUIVLVCZFVLVCZ
        VMFULUMZWMZUUJUWCXDUVSGWIVCZUUCIVCUUEIVCZUWDUWIUVSGOWJZWKVIIUUBEDEGIJKL
        MNOPQRWLZWNUVSUUDVIVCZUWJUUBWOZVIIUUDEUWLWNZWPUUCUUEGINWQWRZUVSUVLUUHVL
        VCZUWEUVMUVSUWFUUGWSVCZUWQUVPUWFTFWTZVFZUUBXAZFUUGXBXCZCUUHXEXCUWHUVSUV
        PUWHTUVPUWFUWGUWSFXFXGVFWKUWDUWEUWHXHUUJUWCUUFUUIFXIXPWRUVSUUPUWBUVTULU
        VSUUPCFUUHUKURZUKURZUWBUVSUUOUXCCUKUVSUUOFUUGUGUHURZUJURZUUHFUKURZUXCUV
        SUUNUXEFUJUVSUUBWAVCUGWAVCZUXHUUNUXEUNUUBXJUXHUVSVPWKZUXIUUBUGUGXKWRUTU
        VSUVOUWRUXFUXGUNUVQUXAFUUGXLXCUVSUUHWAVCZUVOUXGUXCUNUVSUUHUXBXMZUVQUUHF
        XNXOXQUTUVSCWAVCZUVOUXJUXDUWBUNUXLUVSUVRWKUVOUVSUVQWKUXKCFUUHXRWRXSXTYA
        UVSUUMUVTULUMZUWAUUQABCDEFGUUBIJKLMNOPQRSTUAUBYBUVSUUMVLVCZUVTVLVCZUUPV
        LVCZUXMUWAWMUUQXDUVSUWMUXNUWNUWMUWIUWJUULIVCZUXNUWIUWMUWKWKUWOUWMUUKVIV
        CUXQUUDWOVIIUUKEUWLWNWPUUEUULGINWQWRWPUVSUWFUWDUXOUWTUWPFUUFXEXCUVSUVLU
        UOVLVCZUXPUVMUVSUWFUUNWSVCZUXRUWTUVSUWMUXSUWNUUDXAWPFUUNXBXCCUUOXEXCUUM
        UVTUUPYCWRYDYEYF $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  Use ~ geomcau to show that ` G ` is a Cauchy
         sequence, which then converges as ` M ` is complete. $)
      bfplem7 $p |- E. z e. X G ( ~~>m ` M ) z $=
        ( vk cms wcel cca cfv cv clm wbr wrex cme cn wf wa co cr crp c1 clt w3a
        cdiv caddc cexp cmul cle wral cmsmeti bfplem1 pm3.2i cc0 bfplem4 simpli
        rpre ax-mp wne rpne0 redivcli 3pm3.2i cmin bfplem6 wceq recni a1i nnnn0
        cn0 expcl sylancr div13 syl3anc rpexpcl rpdivcl sylancl rpcn syl mulcom
        expm1t mpan nnm1nn0 divmul3 mpbird opreq2d 3eqtrd breqtrrd rgen geomcau
        cc wb mp3an cmscvg mp2an ) HUDUEFHUFUGUEZFCUHHUIUGUJCIUKOHULUEZUMIFUNZU
        ODGVBUPZUQUEZGURUEZGUSUTUJZVAUCUHZFUGXSUSVCUPFUGHUPZXOGXSVDUPZVEUPZVFUJ
        ZUCUMVGXLXMXNHOVHEFHIJKLMNOPQRVIVJXPXQXRDGDUQUEVKDVFUJDEFHIJKLMNOPQRSVL
        VMZXQGUQUETGVNVOZXQGVKVPZTGVQVOZVRTUAVSYCUCUMXSUMUEZXTDGXSUSVTUPZVDUPZV
        EUPZYBVFABDEFGHXSIJKLMNOPQRSTUAUBWAYHYBYAGVBUPZDVEUPZDYLVEUPZYKYHDXGUEZ
        GXGUEZYFUOZYAXGUEZYBYMWBYOYHDYDWCZWDYQYHYPYFGYEWCZYGVJWDZYHYPXSWFUEZYRY
        TXSWEZGXSWGWHZDGYAWIWJYHYLXGUEZYOYMYNWBYHYLURUEZUUEYHYAURUEZXQUUFYHXQUU
        BUUGTUUCGXSWKWHTYAGWLWMYLWNWOYSYLDWPWMYHYLYJDVEYHYLYJWBZYAYJGVEUPWBZYPY
        HUUIYTGXSWQWRYHYRYJXGUEZYQUUHUUIXHUUDYHYPYIWFUEUUJYTXSWSGYIWGWHUUAYAYJG
        WTWJXAXBXCXDXEXOGUCFHINXFXICHFINXJXK $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  The limit of ` G ` is a fixed point of ` F ` . $)
      bfplem8 $p |- ( ( Z e. X /\ G ( ~~>m ` M ) Z ) -> ( F ` Z ) = Z ) $=
        ( vz vk wcel clm cfv wceq cv wa wi eleq1 breq2 anbi12d fveq2 id eqeq12d
        wbr imbi12d cme cmsmeti a1i cn co copab cc0 cli c1 cneg cshi cmul cz cr
        cle cuz wral cc rpcn ax-mp wf wb bfplem1 eqid lmclim2 mp3an12 biimpa 1z
        crp elnnuz opreq1d oprex fvopab4 adantl metcl mp3an1 sylan recnd ancoms
        ffvelrni adantlr eqeltrd opreq2d eqtr4d sylan2br ralrimiva nnex opabex2
        jca elexi climmulc2 syl22anc mul01i syl6breq rpre remulcl sylancr caddc
        ax-1cn nncn shftval4 addcom fveq2d peano2nn 3eqtrd syl2an metge0 opreq1
        0cn syl breq12d opreq2 rcla42v mpi bfplem3 3brtr4d eqbrtrd climsqueeze2
        breqtrrd jca31 syl3anc znegcl mp2an sylib adantr mpbird simpr vex lmuni
        climshft fvex vtoclg anabsi5 ) JHUEZEJGUFUGZURZJDUGZJUHZUCUIZHUEZEUURUU
        NURZUJZUURDUGZUURUHZUKUUMUUOUJZUUQUKUCJHUURJUHZUVAUVDUVCUUQUVEUUSUUMUUT
        UUOUURJHULUURJEUUNUMUNUVEUVBUUPUURJUURJDUOUVEUPUQUSUVAGUTUEZEUVBUUNURZU
        UTUVCUVFUVAGOVAZVBUVAUVGAUIZVCUEZBUIZUVIEUGZUVBGVDZUHUJABVEZVFVGURZUVAU
        VNVHVIZVJVDZVFVGURZUVOUVAUVJUVKFUVLUURGVDZVKVDZUHUJABVEZVFVGURVHVLUEZUD
        UIZUWAUGZVMUEZUWCUVQUGZVMUEZUJVFUWFVNURZUWFUWDVNURZUJZUJZUDVHVOUGZVPUVR
        UVAUWAFVFVKVDZVFVGUVAFVQUEZUVJUVKUVSUHUJABVEZVFVGURZUWBUWCUWOUGZVQUEZUW
        DFUWQVKVDZUHZUJZUDUWLVPUWAUWMVGURUWNUVAFWHUEZUWNTFVRVSZVBUUSUUTUWPUVFVC
        HEVTZUUSUUTUWPWAUVHDEGHIKLMNOPQRWBZABEUWOGHUURNUWOWCZWDWEWFUWBUVAWGVBZU
        VAUXAUDUWLUWCUWLUEZUVAUWCVCUEZUXAUWCWIZUVAUXIUJZUWRUWTUXKUWQUWCEUGZUURG
        VDZVQUXIUWQUXMUHUVAABUWCUVSUXMVCUWOUVIUWCUHZUVLUXLUURGUVIUWCEUOWJZUXFUX
        LUURGWKWLWMZUUSUXIUXMVQUEZUUTUXIUUSUXQUXIUUSUJUXMUXIUXLHUEZUUSUXMVMUEZV
        CHUWCEUXEWSZUVFUXRUUSUXSUVHUXLUURGHNWNWOWPZWQWRWTXAUXKUWDFUXMVKVDZUWSUX
        IUWDUYBUHUVAABUWCUVTUYBVCUWAUXNUVSUXMFVKUXOXBUWAWCFUXMVKWKWLWMZUXKUWQUX
        MFVKUXPXBXCXHXDXEVFFUDUWOUWAVHABVCUVSXFXGABVCUVTXFXGZVFVQYHXIZXJXKFUXCX
        LXMUXGUVAUWKUDUWLUXHUVAUXIUWKUXJUXKUWEUWGUWJUXKUWDUYBVMUYCUXKFVMUEZUXSU
        YBVMUEUXBUYFTFXNVSUUSUXIUXSUUTUXIUUSUXSUYAWRWTFUXMXOXPXAUXKUWFUWCVHXQVD
        ZEUGZUVBGVDZVMUXIUWFUYIUHUVAUXIUWFVHUWCXQVDZUVNUGZUYGUVNUGZUYIUXIVHVQUE
        ZUWCVQUEZUWFUYKUHXRUWCXSZVHUWCUVNABVCUVMXFXGZXTXPUXIUYJUYGUVNUXIUYMUYNU
        YJUYGUHXRUYOVHUWCYAXPYBUXIUYGVCUEZUYLUYIUHUWCYCZABUYGUVMUYIVCUVNUVIUYGU
        HUVLUYHUVBGUVIUYGEUOWJUVNWCZUYHUVBGWKWLYIYDWMZUUSUXIUYIVMUEZUUTUXIUUSVU
        AUXIUYHHUEZUVBHUEZVUAUUSUXIUYQVUBUYRVCHUYGEUXEWSYIZHHUURDPWSZUVFVUBVUCV
        UAUVHUYHUVBGHNWNWOYEWRWTXAUXKUWHUWIUXKVFUYIUWFVNUUSUXIVFUYIVNURZUUTUXIU
        USVUFUXIVUBVUCVUFUUSVUDVUEUVFVUBVUCVUFUVHUYHUVBGHNYFWOYEWRWTUYTYRUXKUWF
        UYIUWDVNUYTUXKUXLDUGZUVBGVDZUYBUYIUWDVNUUSUXIVUHUYBVNURZUUTUXIUUSVUIUXI
        UXRUUSVUIUXTUXRUUSUJUVIDUGZUVKDUGZGVDZFUVIUVKGVDZVKVDZVNURZBHVPAHVPVUIU
        BVUOVUIVUGVUKGVDZFUXLUVKGVDZVKVDZVNURABUXLUURHHUVIUXLUHZVULVUPVUNVURVNV
        USVUJVUGVUKGUVIUXLDUOWJVUSVUMVUQFVKUVIUXLUVKGYGXBYJUVKUURUHZVUPVUHVURUY
        BVNVUTVUKUVBVUGGUVKUURDUOXBVUTVUQUXMFVKUVKUURUXLGYKXBYJYLYMWPWRWTUXKUYH
        VUGUVBGUXIUYHVUGUHUVADEGUWCHIKLMNOPQRYNWMWJUYCYOYPXHYSXDXEVFUDUWAUVQVHU
        YDUVNUVPVJWKUYEYQYTUVPVLUEZVFVQUEUVRUVOWAUWBVVAWGVHUUAVSYHVFUVNUVPVQUYP
        UUIUUBUUCUUSUVGUVOWAZUUTUUSUVFUXDVUCVVBUVFUUSUVHVBUXDUUSUXEVBVUEABEUVNG
        HUVBNUYSWDYTUUDUUEUUSUUTUUFUVBUURGEUURDUUJUCUUGUUHYTUUKUUL $.
        $( [5-Nov-2013] $) $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  The fixed point is unique. $)
      bfplem9 $p |- ( ( ( Z e. X /\ W e. X ) /\
                        ( ( F ` Z ) = Z /\ ( F ` W ) = W ) ) -> Z = W ) $=
        ( wcel wa cfv wceq co cc0 cle wbr clt wn wb cr 0re cmsmeti metcl mp3an1
        cme eqlelt sylancr adantr ltnr syl cmul ad2antrr crp rpre ax-mp remulcl
        metge0 opreq12 adantl wral fveq2 opreq1d opreq1 opreq2d breq12d rcla42v
        cv opreq2 mpi eqbrtrrd c1 1re w3a ltmul1 mpbii mp3an12 sylan adantlr cc
        recn mulid2 3syl breqtrd lelttrd mtand mpbir2and eqcomd meteq0 mpbid )
        KIUDZHIUDZUEZKDUFZKUGHDUFZHUGUEZUEZKHGUHZUIUGZKHUGZXKUIXLXKUIXLUGZUIXLU
        JUKZUIXLULUKZUMZXGXOXPXRUEUNZXJXGUIUOUDXLUOUDZXSUPGUTUDZXEXFXTGPUQZKHGI
        OURUSZUIXLVAVBVCXGXPXJYAXEXFXPYBKHGIOVLUSVCXKXQXLXLULUKZXGYDUMZXJXGXTYE
        YCXLVDVEVCXKXQUEZXLFXLVFUHZXLXGXTXJXQYCVGZYFFUOUDZXTYGUOUDFVHUDYIUAFVIV
        JZYHFXLVKVBYHXKXLYGUJUKXQXKXHXIGUHZXLYGUJXJYKXLUGXGXHKXIHGVMVNXGYKYGUJU
        KZXJXGAWBZDUFZBWBZDUFZGUHZFYMYOGUHZVFUHZUJUKZBIVOAIVOYLUCYTYLXHYPGUHZFK
        YOGUHZVFUHZUJUKABKHIIYMKUGZYQUUAYSUUCUJUUDYNXHYPGYMKDVPVQUUDYRUUBFVFYMK
        YOGVRVSVTYOHUGZUUAYKUUCYGUJUUEYPXIXHGYOHDVPVSUUEUUBXLFVFYOHKGWCVSVTWAWD
        VCWEVCYFYGWFXLVFUHZXLULXGXQYGUUFULUKZXJXGXTXQUUGYCYIWFUOUDZXTXQUEZUUGYJ
        WGYIUUHUUIWHFWFULUKUUGUBFWFXLWIWJWKWLWMXGUUFXLUGZXJXQXGXTXLWNUDUUJYCXLW
        OXLWPWQVGWRWSWTXAXBXGXMXNUNZXJYAXEXFUUKYBKHGIOXCUSVCXD $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp . ` F ` has exactly one fixed point. $)
      bfplem10 $p |- E! z e. X ( F ` z ) = z $=
        ( vw cv cfv wceq wreu wrex wa wi wral fveq2 id eqeq12d reu4 clm bfplem7
        wbr wcel bfplem8 ex reximia ax-mp bfplem9 rgen2a mpbir2an ) CUDZEUEZVGU
        FZCIUGVICIUHZVIUCUDZEUEZVKUFZUIZVGVKUFZUJZUCIUKCIUKVIVMCUCIVOVHVLVGVKVG
        VKEULVOUMUNUOFVGHUPUEURZCIUHVJABCDEFGHIJKLMNOPQRSTUAUBUQVQVICIVGIUSZVQV
        IABDEFGHIJVGKLMNOPQRSTUAUBUTVAVBVCVPCUCIVRVKIUSUIVNVOABDEFGHVKIJVGKLMNO
        PQRSTUAUBVDVAVEVF $.
        $( [2-Sep-2009] $)
    $}

    ${
      $d M a b c $.  $d K a b c $.  $d F u v $.  $d X u v $.  $d Y u v $.
      bfplem.2a $e |- M e. CMet $.
      bfplem.4a $e |- Y e. X $.
      bfplem.7a $e |- K e. RR+ $.
      bfplem.8a $e |- K < 1 $.
      $( Lemma for ~ bfp .  Apply the deduction theorem. $)
      bfplem11 $p |- ( ( F : X --> X /\ A. x e. X A. y e. X
                            ( ( F ` x ) M ( F ` y ) ) <_ ( K x. ( x M y ) ) )
                                        -> E! z e. X ( F ` z ) = z ) $=
        ( vu vv cv cfv co cle wcel va vb vc cmul wbr wral wceq wreu csn cxp cif
        wf wa fveq1 eqeq1d reubidv copab2 cn cseq1 fveq2 opreq1d opreq1 opreq2d
        breq12d opreq2 cbvral2v opreq12d breq1d 2ralbidv syl5bb anbi12d fconst6
        feq1 cc0 elexi fvconst2 opreqan12d cme cmsmeti met0 mp2an syl6eq cr crp
        rpre ax-mp a1i rpge0 metcl mp3an1 metge0 mulge0 syl22anc eqbrtrd rgen2a
        pm3.2i elimhyp simpli eqid simpri bfplem10 dedth ) GGDULZAPZDQZBPZDQZFR
        ZEXDXFFRZUDRZSUEZBGUFAGUFZUMZCPZDQZXNUGZCGUHXNXMDGHUIZUJZUKZQZXNUGZCGUH
        DXRDXSUGZXPYACGYBXOXTXNXNDXSUNUOUPNOCHHXSQFRZXSUAPZGTUBPGTUMUCPYDXSQUGU
        MUAUBUCUQURXQUJUSRZEFGHUAUBUCIJGGXSULZNPZXSQZOPZXSQZFRZEYGYIFRZUDRZSUEZ
        OGUFNGUFZXMYFYOUMGGXRULZYGXRQZYIXRQZFRZYMSUEZOGUFNGUFZUMDXRYBXCYFXLYOGG
        DXSVMXLYGDQZYIDQZFRZYMSUEZOGUFNGUFYBYOXKUUEUUBXGFRZEYGXFFRZUDRZSUEABNOG
        GXDYGUGZXHUUFXJUUHSUUIXEUUBXGFXDYGDUTVAUUIXIUUGEUDXDYGXFFVBVCVDXFYIUGZU
        UFUUDUUHYMSUUJXGUUCUUBFXFYIDUTVCUUJUUGYLEUDXFYIYGFVEVCVDVFYBUUEYNNOGGYB
        UUDYKYMSYBUUBYHUUCYJFYGDXSUNYIDXSUNVGVHVIVJVKXRXSUGZYPYFUUAYOGGXRXSVMUU
        KYTYNNOGGUUKYSYKYMSUUKYQYHYRYJFYGXRXSUNYIXRXSUNVGVHVIVKYPUUAGHGKVLYTNOG
        YGGTZYIGTZUMZYSVNYMSUUNYSHHFRZVNUULUUMYQHYRHFGHYGHGKVOZVPGHYIUUPVPVQFVR
        TZHGTUUOVNUGFJVSZKHFGIVTWAWBUUNEWCTZVNESUEZYLWCTZVNYLSUEZVNYMSUEUUSUUNE
        WDTZUUSLEWEWFWGUUTUUNUVCUUTLEWHWFWGUUQUULUUMUVAUURYGYIFGIWIWJUUQUULUUMU
        VBUURYGYIFGIWKWJEYLWLWMWNWOWPWQZWRKYEWSYCWSLMYFYOUVDWTXAXB $.
        $( [2-Sep-2009] $)
    $}

    $( Banach fixed point theorem, also known as contraction mapping theorem.
       A contraction on a complete metric space has a unique fixed point.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    bfp $p |- ( ( ( M e. CMet /\ X =/= (/) ) /\ ( K e. RR+ /\ K < 1 ) /\
                        ( F : X --> X /\ A. x e. X A. y e. X
                          ( ( F ` x ) M ( F ` y ) ) <_ ( K x. ( x M y ) ) ) )
                                        -> E! z e. X ( F ` z ) = z ) $=
      ( wcel wa crp c1 clt wbr co cle wral wceq cc0 cn vw vf vj vk cms c0 wf cv
      wne cfv cmul wreu cdm wi wex neeq1i n0 bitri cop csn cxp cif c2 cdiv dmeq
      dmeqd feq2d feq3 syl bitrd opreq opreq2d breq12d raleqbidv anbi12d reueq1
      wb imbi12d opreq1 breq2d 2ralbidv anbi2d imbi1d eleq1 eleq2d cc 0cn elexi
      eqid snnz dmxp ax-mp dmeqi dmsnop eqtr2i cme vex xpsn xpeq1i snex fconst6
      cr 0re oprvconst2 elsni eqcomd sylan9eq 2thd caddc leidi addid1i breqtrri
      w3a a1i 3adant3 ancoms 3adant2 3adant1 opreq12d 3brtr4d eqeltrri cca wrex
      ismeti clm fconst2 biimpi adantl fconst fvconst2 rcla4ev sylancr eleqtrri
      snid breq1 pm3.2i elimhyp simpli simpri raleqi mp3an 1nn met0 mp2an rpgt0
      lmbrnns syl5eqbr a1d ralrimiva ralbidv mprgbir syl6eqbr breq2 iscms2i 2re
      adantr elrpii rpreccl halflt1 bfplem11 dedth2h exp31 exlimdv syl5bi imp31
      2pos feq23i ralbii anbi12i 3imtr4g 3impia ) FUEIZGUFUIZJZEKIZELMNZJZGGDUG
      ZAUHZDUJZBUHZDUJZFOZEUVSUWAFOZUKOZPNZBGQZAGQZJZCUHZDUJUWJRZCGULZUVNUVQJFU
      MZUMZUWNDUGZUWFBUWNQZAUWNQZJZUWKCUWNULZUWIUWLUVLUVMUVQUWRUWSUNZUVMUAUHZUW
      NIZUAUOZUVLUVQUWTUNZUVMUWNUFUIUXCGUWNUFHUPUAUWNUQURUVLUXBUXDUAUVLUXBUVQUW
      TUVLUXBJZUVQUWTUXEFUXAUXAUSUTZSUTZVAZVBZUMZUMZUXKDUGZUVTUWBUXIOZEUVSUWAUX
      IOZUKOZPNZBUXKQZAUXKQZJZUWKCUXKULZUNUXLUXMUVQELVCVDOZVBZUXNUKOZPNZBUXKQAU
      XKQZJZUXTUNFEUXHUYAFUXIRZUWRUXSUWSUXTUYGUWOUXLUWQUXRUYGUWOUXKUWNDUGZUXLUY
      GUWNUXKUWNDUYGUWMUXJFUXIVEVFZVGUYGUWNUXKRZUYHUXLVQUYIUWNUXKUXKDVHVIVJUYGU
      WPUXQAUWNUXKUYIUYGUWFUXPBUWNUXKUYIUYGUWCUXMUWEUXOPUVTUWBFUXIVKUYGUWDUXNEU
      KUVSUWAFUXIVKVLVMVNVNVOUYGUYJUWSUXTVQUYIUWKCUWNUXKVPVIVREUYBRZUXSUYFUXTUY
      KUXRUYEUXLUYKUXPUYDABUXKUXKUYKUXOUYCUXMPEUYBUXNUKVSVTWAWBWCABCDUYBUXIUXKU
      XAUXKWIUXIUEIZUXAUXKIZUXEUYLUYMJUXHUEIZUXAUXHUMZUMZIZJFUXHUYGUVLUYLUXBUYM
      FUXIUEWDUYGUWNUXKUXAUYIWEVOUXHUXIRZUYNUYLUYQUYMUXHUXIUEWDUYRUYPUXKUXAUYRU
      YOUXJUXHUXIVEVFWEVOUYNUYQCUXHUBUXAUTZUYPUXFUMZUYSUYOUXFUXGUFUIUYOUXFRSSWF
      WGWHZWJUXFUXGWKWLWMZUXAUXAWNZWOZUYSUYSVAZUXGVAZUXHWPVUEUXFUXGUXAUXAUAWQZV
      UGWRWSABCVUFUYSUXAWTVUESXBXCXAUVSUYSIZUWAUYSIZJUVSUWAVUFOZSRZUVSUWARUYSUY
      SSUVSUWAVUAXDZVUHVUIUVSUXAUWAUVSUXAXEVUIUWAUXAUWAUXAXEXFXGXHVUHVUIUWJUYSI
      ZXMZSSSXIOZVUJUWJUVSVUFOZUWJUWAVUFOZXIOPSVUOPNVUNSSVUOPSXCXJSWGXKXLXNVUHV
      UIVUKVUMVULXOVUNVUPSVUQSXIVUHVUMVUPSRZVUIVUMVUHVURUYSUYSSUWJUVSVUAXDXPXQV
      UIVUMVUQSRZVUHVUMVUIVUSUYSUYSSUWJUWAVUAXDXPXRXSXTYDYAZUBUHZUXHYBUJIZTUYSV
      VAUGZJZUXAUYSIZVVAUXAUXHYEUJZNZVVAUWJVVFNZCUYSYCUXAVUGYNZVVDVVATUYSVAZUXA
      VVFVVCVVAVVJRZVVBVVCVVKTUXAVVAVUGYFYGYHVVJUXAVVFNZUCUHZUDUHZPNZUXAUXAUXHO
      ZUVSMNZUNZUDTQZUCTYCZAKUXHWPIZVVETUYSVVJUGVVLVVTAKQVQVUTVVITUXAVUGYIAUXAU
      XHUXAUCUDVVJUYSVUDVVNTIZVVNVVJUJUXATUXAVVNVUGYJXFUUFUUAUVSKIZLTILVVNPNZVV
      QUNZUDTQZVVTUUBVWCVWEUDTVWCVWEVWBVWCVVQVWDVWCVVPSUVSMVWAVVEVVPSRVUTVVIUXA
      UXHUYSVUDUUCUUDUVSUUEUUGUUHUUPUUIVVSVWFUCLTVVMLRZVVRVWEUDTVWGVVOVWDVVQVVM
      LVVNPYOWCUUJYKYLUUKUULVVHVVGCUXAUYSUWJUXAVVAVVFUUMYKYLUUNUXAUYTUYPUXAUYSU
      YTVVIVUCYMVUBYMYPYQZYRUYLUYMVWHYSUYBKIZUYBLMNZUVQVWIVWJJUYAKIZUYALMNZJEUY
      AUYKUVOVWIUVPVWJEUYBKWDEUYBLMYOVOUYAUYBRVWKVWIVWLVWJUYAUYBKWDUYAUYBLMYOVO
      VWKVWLVCKIVWKVCUUOUVFUUQVCUURWLUUSYPYQZYRVWIVWJVWMYSUUTUVAUVBUVCUVDUVEUVR
      UWOUWHUWQGGUWNUWNDHHUVGUWHUWGAUWNQUWQUWGAGUWNHYTUWGUWPAUWNUWFBGUWNHYTUVHU
      RUVIGUWNRUWLUWSVQHUWKCGUWNVPWLUVJUVK $.
      $( [2-Sep-2009] $)
  $}

