$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Banach Fixed Point Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F a b c k w x y z $.  $d X a b c k w x y z $.  $d Z x y z $.
    $d D k u v $.  $d N a b c u v x y z $.  $d Y a b c x y z $.  $d W x y z $.
    $d f j k w x z $.  $d G k u v x y z $.  $d M k u v w x y z $.
    $d K u v k w x y z $.
    bfp.1 $e |- X = dom dom M $.

    ${
      bfplem.2 $e |- M e. CMet $.
      bfplem.3 $e |- F : X --> X $.
      bfplem.4 $e |- Y e. X $.
      bfplem.5 $e |- G = ( { <. <. a , b >. , c >. | ( ( a e. X /\ b e. X )
                                /\ c = ( F ` a ) ) } seq1 ( NN X. { Y } ) ) $.
      $( Lemma for ~ bfp .  We construct a sequence ` G ` by repeatedly
         applying ` F ` to a starting value ` Y ` . $)
      bfplem1 $p |- G : NN --> X $=
        ( cn wf cv wcel wa cfv wceq copab2 csn cxp cseq1 co fconst6 eqid
        ffvelrni adantr foprab cdm cvv cms elisseti dmex eqeltri oprabex2 nnex
        snex xpex seq1f mp2an feq1i mpbir ) NDBONDFPZDQZGPDQZRHPVEASZTRFGHUAZNE
        UBZUCZUDUEZOZNDVKODDUCDVIOVMNEDLUFFGHDDVHDVIVIUGZVFVHDQVGDDVEAKUHUIUJDV
        IVKFGHDDVHVIDCUKZUKULIVOCCUMJUNUOUOUPZVPVNUQNVJUREUSUTVAVBNDBVLMVCVD $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  The first element in the sequence is ` Y ` . $)
      bfplem2 $p |- ( G ` 1 ) = Y $=
        ( c1 cfv cv wcel wa wceq copab2 cn csn cxp cseq1 co fveq1i cdm cvv cms
        elisseti dmex eqeltri eqid oprabex2 nnex snex xpex seq11 1nn fvconst2
        ax-mp 3eqtri ) NBONFPZDQGPDQRHPVCAOZSRFGHTZUAEUBZUCZUDUEZONVGOZENBVHMUF
        VEVGFGHDDVDVEDCUGZUGUHIVJCCUIJUJUKUKULZVKVEUMUNUAVFUOEUPUQURNUAQVIESUSU
        AENEDLUJUTVAVB $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  The next element in the sequence is obtained by
         applying ` F ` to the previous element. $)
      bfplem3 $p |- ( N e. NN -> ( G ` ( N + 1 ) ) = ( F ` ( G ` N ) ) ) $=
        ( vx vy vz cn wcel c1 caddc co cfv csn cxp cv wa wceq copab2 cseq1 cdm
        cvv cms elisseti dmex eqeltri eqid oprabex2 nnex snex xpex seq1p1
        fveq1i a1i opreq1d 3eqtr4d bfplem1 ffvelrni peano2nn fvconst2 syl
        syl6eqel fvex fveq2 eqidd eleq1 bi2anan9 eqeqan12rd anbi12d
        cbvoprab12v eqeq1 anbi2d cbvoprab3v eqtri oprabval2 syl11anc eqtrd ) DR
        SZDTUAUBZBUCZDBUCZWIRFUDZUEZUCZGUFZESZHUFZESZUGZIUFZWOAUCZUHZUGZGHIUIZU
        BZWKAUCZWHWIXDWMUJUBZUCZDXGUCZWNXDUBWJXEDXDWMGHIEEXAXDECUKZUKULJXJCCUMK
        UNUOUOUPZXKXDUQURRWLUSFUTVAVBWJXHUHWHWIBXGNVCVDWHWKXIWNXDWKXIUHWHDBXGNV
        CVDVEVFWHWKESWNESXEXFUHREDBABCEFGHIJKLMNVGVHWHWNFEWHWIRSWNFUHDVIRFWIFEM
        UNVJVKMVLOPQWKWNEEOUFZAUCZXFXDXFWKAVMXLWKAVNPUFZWNUHXFVOXDXLESZXNESZUGZ
        WTXMUHZUGZOPIUIXQQUFZXMUHZUGZOPQUIXCXSGHIOPWOXLUHZWQXNUHZUGWSXQXBXRYCWP
        XOYDWRXPWOXLEVPWQXNEVPVQYDYCWTWTXAXMYDWTVOWOXLAVNVRVSVTXSYBOPIQWTXTUHXR
        YAXQWTXTXMWAWBWCWDWEWFWG $.
        $( [2-Sep-2009] $)

      bfplem.6 $e |- D = ( Y M ( F ` Y ) ) $.
      $( Lemma for ~ bfp .  Define the initial distance ` D ` . $)
      bfplem4 $p |- ( D e. RR /\ 0 <_ D ) $=
        ( cr wcel cc0 cle wbr cfv co cme cmsmeti wf ffvelrn mp2an metcl mp3an
        eqeltri metge0 breqtrri pm3.2i ) APQRASTAFFBUAZDUBZPODUCQZFEQZUNEQZUOPQ
        DKUDZMEEBUEUQURLMEEFBUFUGZFUNDEJUHUIUJRUOASUPUQURRUOSTUSMUTFUNDEJUKUIOU
        LUM $.
        $( [2-Sep-2009] $)

      bfplem.7 $e |- K e. RR+ $.
      bfplem.8 $e |- K < 1 $.
      bfplem.9 $e |- A. x e. X A. y e. X
                            ( ( F ` x ) M ( F ` y ) ) <_ ( K x. ( x M y ) ) $.
      $( Lemma for ~ bfp .  Recursively bound the distance between consecutive
         elements of the sequence ` G ` based on the contraction property. $)
      bfplem5 $p |- ( N e. NN -> ( ( G ` ( N + 1 ) ) M ( G ` ( ( N + 1 ) + 1 )
                        ) ) <_ ( K x. ( ( G ` N ) M ( G ` ( N + 1 ) ) ) ) ) $=
        ( cn wcel c1 caddc co cfv cmul cle bfplem3 wceq peano2nn syl opreq12d
        wbr bfplem1 ffvelrni wa cv wral fveq2 opreq1d opreq1 opreq2d breq12d
        opreq2 rcla42v mpi syl11anc eqbrtrd ) HUCUDZHUEUFUGZEUHZVMUEUFUGEUHZGUG
        HEUHZDUHZVNDUHZGUGZFVPVNGUGZUIUGZUJVLVNVQVOVRGDEGHIJKLMNOPQRUKVLVMUCUDZ
        VOVRULHUMZDEGVMIJKLMNOPQRUKUNUOVLVPIUDZVNIUDZVSWAUJUPZUCIHEDEGIJKLMNOPQ
        RUQZURVLWBWEWCUCIVMEWGURUNWDWEUSAUTZDUHZBUTZDUHZGUGZFWHWJGUGZUIUGZUJUPZ
        BIVAAIVAWFUBWOWFVQWKGUGZFVPWJGUGZUIUGZUJUPABVPVNIIWHVPULZWLWPWNWRUJWSWI
        VQWKGWHVPDVBVCWSWMWQFUIWHVPWJGVDVEVFWJVNULZWPVSWRWAUJWTWKVRVQGWJVNDVBVE
        WTWQVTFUIWJVNVPGVGVEVFVHVIVJVK $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  Bound the distance between consecutive elements of
         the sequence ` G ` by applying ~ bfplem5 inductively. $)
      bfplem6 $p |- ( N e. NN -> ( ( G ` N ) M ( G ` ( N + 1 ) ) )
                                      <_ ( D x. ( K ^ ( N - 1 ) ) ) ) $=
        ( vu vv cv cfv c1 caddc co cmin cexp cmul cle wbr wceq fveq2 opreq1
        fveq2d opreq12d opreq2d breq12d bfplem2 cn wcel 1nn bfplem3 ax-mp
        fveq2i eqtri opreq12i eqtr4i cr cc0 bfplem4 simpli ax1cn subidi
        opreq2i cc crp rpcn exp0 recni mulid1i eqtr2i eqle mp2an eqbrtri wa wi
        cme cmsmeti a1i bfplem1 ffvelrni peano2nn syl metcl syl111anc cn0 rpre
        nnm1nn0 reexpcl sylancr remulcl rpge0 jca lemul2aALT nncn addsub expp1
        recnd mulcom sylancl 3eqtrd mul12 eqtrd breq2d sylibrd bfplem5 letr
        mpand syld nnind ) UCUEZEUFZYEUGUHUIZEUFZGUIZCFYEUGUJUIZUKUIZULUIZUMUNU
        GEUFZUGUGUHUIZEUFZGUIZCFUGUGUJUIZUKUIZULUIZUMUNUDUEZEUFZYTUGUHUIZEUFZGU
        IZCFYTUGUJUIZUKUIZULUIZUMUNZUUCUUBUGUHUIZEUFZGUIZCFUUBUGUJUIZUKUIZULUIZ
        UMUNZHEUFZHUGUHUIZEUFZGUIZCFHUGUJUIZUKUIZULUIZUMUNUCUDHYEUGUOZYIYPYLYSU
        MUVCYFYMYHYOGYEUGEUPUVCYGYNEYEUGUGUHUQURUSUVCYKYRCULUVCYJYQFUKYEUGUGUJU
        QUTUTVAYEYTUOZYIUUDYLUUGUMUVDYFUUAYHUUCGYEYTEUPUVDYGUUBEYEYTUGUHUQURUSU
        VDYKUUFCULUVDYJUUEFUKYEYTUGUJUQUTUTVAYEUUBUOZYIUUKYLUUNUMUVEYFUUCYHUUJG
        YEUUBEUPUVEYGUUIEYEUUBUGUHUQURUSUVEYKUUMCULUVEYJUULFUKYEUUBUGUJUQUTUTVA
        YEHUOZYIUUSYLUVBUMUVFYFUUPYHUURGYEHEUPUVFYGUUQEYEHUGUHUQURUSUVFYKUVACUL
        UVFYJUUTFUKYEHUGUJUQUTUTVAYPCYSUMYPJJDUFZGUICYMJYOUVGGDEGIJKLMNOPQRVBZY
        OYMDUFZUVGUGVCVDYOUVIUOVEDEGUGIJKLMNOPQRVFVGYMJDUVHVHVIVJSVKCVLVDZCYSUO
        CYSUMUNUVJVMCUMUNCDEGIJKLMNOPQRSVNVOZYSCUGULUICYRUGCULYRFVMUKUIZUGYQVMF
        UKUGVPVQVRFVSVDZUVLUGUOFVTVDZUVMTFWAVGZFWBVGVIVRCCUVKWCZWDWECYSWFWGWHYT
        VCVDZUUHFUUDULUIZUUNUMUNZUUOUVQUUHUVRFUUGULUIZUMUNZUVSUVQUUDVLVDZUUGVLV
        DZFVLVDZVMFUMUNZWIZUUHUWAWJUVQGWKVDZUUAIVDUUCIVDZUWBUWGUVQGOWLZWMVCIYTE
        DEGIJKLMNOPQRWNZWOUVQUUBVCVDZUWHYTWPZVCIUUBEUWJWOZWQUUAUUCGINWRWSZUVQUV
        JUUFVLVDZUWCUVKUVQUWDUUEWTVDZUWOUVNUWDTFXAZVGZYTXBZFUUEXCXDZCUUFXEXDUWF
        UVQUVNUWFTUVNUWDUWEUWQFXFXGVGWMUUDUUGFXHWSUVQUUNUVTUVRUMUVQUUNCFUUFULUI
        ZULUIZUVTUVQUUMUXACULUVQUUMFUUEUGUHUIZUKUIZUUFFULUIZUXAUVQUULUXCFUKUVQY
        TVSVDUGVSVDZUXFUULUXCUOYTXIUXFUVQVPWMZUXGYTUGUGXJWSUTUVQUVMUWPUXDUXEUOU
        VOUWSFUUEXKXDUVQUUFVSVDZUVMUXEUXAUOUVQUUFUWTXLZUVOUUFFXMXNXOUTUVQCVSVDZ
        UVMUXHUXBUVTUOUXJUVQUVPWMUVMUVQUVOWMUXICFUUFXPWSXQXRXSUVQUUKUVRUMUNZUVS
        UUOABCDEFGYTIJKLMNOPQRSTUAUBXTUVQUUKVLVDZUVRVLVDZUUNVLVDZUXKUVSWIUUOWJU
        VQUWKUXLUWLUWKUWGUWHUUJIVDZUXLUWGUWKUWIWMUWMUWKUUIVCVDUXOUUBWPVCIUUIEUW
        JWOWQUUCUUJGINWRWSWQUVQUWDUWBUXMUWRUWNFUUDXEXDUVQUVJUUMVLVDZUXNUVKUVQUW
        DUULWTVDZUXPUWRUVQUWKUXQUWLUUBXBWQFUULXCXDCUUMXEXDUUKUVRUUNYAWSYBYCYD
        $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  Use ~ geomcau to show that ` G ` is a Cauchy
         sequence, which then converges as ` M ` is complete. $)
      bfplem7 $p |- E. z e. X G ( ~~>m ` M ) z $=
        ( vk cms wcel cca cfv cv clm wbr wrex cme cn wf wa cdiv co cr crp c1
        clt w3a caddc cexp cmul cle wral cmsmeti bfplem1 pm3.2i cc0 bfplem4
        simpli rpre ax-mp wne rpne0 redivcli 3pm3.2i cmin bfplem6 cc wceq
        recni a1i cn0 nnnn0 expcl sylancr div13 syl111anc rpexpcl rpdivcl
        sylancl rpcn syl mulcom expm1t mpan wb nnm1nn0 divmul3 mpbird opreq2d
        3eqtrd breqtrrd rgen geomcau mp3an cmscvg mp2an ) HUDUEFHUFUGUEZFCUHHUI
        UGUJCIUKOHULUEZUMIFUNZUODGUPUQZURUEZGUSUEZGUTVAUJZVBUCUHZFUGXSUTVCUQFUG
        HUQZXOGXSVDUQZVEUQZVFUJZUCUMVGXLXMXNHOVHEFHIJKLMNOPQRVIVJXPXQXRDGDURUEV
        KDVFUJDEFHIJKLMNOPQRSVLVMZXQGURUETGVNVOZXQGVKVPZTGVQVOZVRTUAVSYCUCUMXSU
        MUEZXTDGXSUTVTUQZVDUQZVEUQZYBVFABDEFGHXSIJKLMNOPQRSTUAUBWAYHYBYAGUPUQZD
        VEUQZDYLVEUQZYKYHDWBUEZGWBUEZYFUOZYAWBUEZYBYMWCYOYHDYDWDZWEYQYHYPYFGYEW
        DZYGVJWEZYHYPXSWFUEZYRYTXSWGZGXSWHWIZDGYAWJWKYHYLWBUEZYOYMYNWCYHYLUSUEZ
        UUEYHYAUSUEZXQUUFYHXQUUBUUGTUUCGXSWLWITYAGWMWNYLWOWPYSYLDWQWNYHYLYJDVEY
        HYLYJWCZYAYJGVEUQWCZYPYHUUIYTGXSWRWSYHYRYJWBUEZYQUUHUUIWTUUDYHYPYIWFUEU
        UJYTXSXAGYIWHWIUUAYAYJGXBWKXCXDXEXFXGXOGUCFHINXHXICHFINXJXK $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  The limit of ` G ` is a fixed point of ` F ` . $)
      bfplem8 $p |- ( ( Z e. X /\ G ( ~~>m ` M ) Z ) -> ( F ` Z ) = Z ) $=
        ( vz vk wcel clm cfv wbr wceq cv wa wi eleq1 breq2 anbi12d fveq2 id
        eqeq12d imbi12d cme cmsmeti a1i cn co copab cc0 cli c1 cneg cshi cmul
        cz cr cle cuz wral cc crp rpcn ax-mp wf wb bfplem1 eqid lmclim2
        mp3an12 biimpa 1z elnnuz opreq1d oprex fvopab4 adantl ffvelrni metcl
        mp3an1 sylan recnd ancoms adantlr eqeltrd opreq2d eqtr4d jca sylan2br
        r19.21aiva nnex opabex2 0cn elisseti climmulc2 syl22anc mul01i
        syl6breq rpre remulcl sylancr caddc ax1cn nncn shftval4 addcom fveq2d
        peano2nn syl 3eqtrd syl2an metge0 breqtrrd opreq1 breq12d opreq2
        rcla42v mpi bfplem3 3brtr4d eqbrtrd jca31 climsqueeze2 syl111anc
        znegcl climshfti sylib adantr mpbird simpr fvex visset lmuni vtoclg
        anabsi5 ) JHUEZEJGUFUGZUHZJDUGZJUIZUCUJZHUEZEUUQUUMUHZUKZUUQDUGZUUQUIZU
        LUULUUNUKZUUPULUCJHUUQJUIZUUTUVCUVBUUPUVDUURUULUUSUUNUUQJHUMUUQJEUUMUNU
        OUVDUVAUUOUUQJUUQJDUPUVDUQURUSUUTGUTUEZEUVAUUMUHZUUSUVBUVEUUTGOVAZVBUUT
        UVFAUJZVCUEZBUJZUVHEUGZUVAGVDZUIUKABVEZVFVGUHZUUTUVMVHVIZVJVDZVFVGUHZUV
        NUUTUVIUVJFUVKUUQGVDZVKVDZUIUKABVEZVFVGUHVHVLUEZUDUJZUVTUGZVMUEZUWBUVPU
        GZVMUEZUKVFUWEVNUHZUWEUWCVNUHZUKZUKZUDVHVOUGZVPUVQUUTUVTFVFVKVDZVFVGUUT
        FVQUEZUVIUVJUVRUIUKABVEZVFVGUHZUWAUWBUWNUGZVQUEZUWCFUWPVKVDZUIZUKZUDUWK
        VPUVTUWLVGUHUWMUUTFVRUEZUWMTFVSVTZVBUURUUSUWOUVEVCHEWAZUURUUSUWOWBUVGDE
        GHIKLMNOPQRWCZABEUWNGHUUQNUWNWDZWEWFWGUWAUUTWHVBZUUTUWTUDUWKUWBUWKUEZUU
        TUWBVCUEZUWTUWBWIZUUTUXHUKZUWQUWSUXJUWPUWBEUGZUUQGVDZVQUXHUWPUXLUIUUTAB
        UWBUVRUXLVCUWNUVHUWBUIZUVKUXKUUQGUVHUWBEUPWJZUXEUXKUUQGWKWLWMZUURUXHUXL
        VQUEZUUSUXHUURUXPUXHUURUKUXLUXHUXKHUEZUURUXLVMUEZVCHUWBEUXDWNZUVEUXQUUR
        UXRUVGUXKUUQGHNWOWPWQZWRWSWTXAUXJUWCFUXLVKVDZUWRUXHUWCUYAUIUUTABUWBUVSU
        YAVCUVTUXMUVRUXLFVKUXNXBUVTWDFUXLVKWKWLWMZUXJUWPUXLFVKUXOXBXCXDXEXFVFFU
        DUWNUVTVHABVCUVRXGXHABVCUVSXGXHZVFVQXIXJZXKXLFUXBXMXNUXFUUTUWJUDUWKUXGU
        UTUXHUWJUXIUXJUWDUWFUWIUXJUWCUYAVMUYBUXJFVMUEZUXRUYAVMUEUXAUYETFXOVTUUR
        UXHUXRUUSUXHUURUXRUXTWSWTFUXLXPXQXAUXJUWEUWBVHXRVDZEUGZUVAGVDZVMUXHUWEU
        YHUIUUTUXHUWEVHUWBXRVDZUVMUGZUYFUVMUGZUYHUXHVHVQUEZUWBVQUEZUWEUYJUIXSUW
        BXTZVHUWBUVMABVCUVLXGXHZYAXQUXHUYIUYFUVMUXHUYLUYMUYIUYFUIXSUYNVHUWBYBXQ
        YCUXHUYFVCUEZUYKUYHUIUWBYDZABUYFUVLUYHVCUVMUVHUYFUIUVKUYGUVAGUVHUYFEUPW
        JUVMWDZUYGUVAGWKWLYEYFWMZUURUXHUYHVMUEZUUSUXHUURUYTUXHUYGHUEZUVAHUEZUYT
        UURUXHUYPVUAUYQVCHUYFEUXDWNYEZHHUUQDPWNZUVEVUAVUBUYTUVGUYGUVAGHNWOWPYGW
        SWTXAUXJUWGUWHUXJVFUYHUWEVNUURUXHVFUYHVNUHZUUSUXHUURVUEUXHVUAVUBVUEUURV
        UCVUDUVEVUAVUBVUEUVGUYGUVAGHNYHWPYGWSWTUYSYIUXJUWEUYHUWCVNUYSUXJUXKDUGZ
        UVAGVDZUYAUYHUWCVNUURUXHVUGUYAVNUHZUUSUXHUURVUHUXHUXQUURVUHUXSUXQUURUKU
        VHDUGZUVJDUGZGVDZFUVHUVJGVDZVKVDZVNUHZBHVPAHVPVUHUBVUNVUHVUFVUJGVDZFUXK
        UVJGVDZVKVDZVNUHABUXKUUQHHUVHUXKUIZVUKVUOVUMVUQVNVURVUIVUFVUJGUVHUXKDUP
        WJVURVULVUPFVKUVHUXKUVJGYJXBYKUVJUUQUIZVUOVUGVUQUYAVNVUSVUJUVAVUFGUVJUU
        QDUPXBVUSVUPUXLFVKUVJUUQUXKGYLXBYKYMYNWQWSWTUXJUYGVUFUVAGUXHUYGVUFUIUUT
        DEGUWBHIKLMNOPQRYOWMWJUYBYPYQXDYRXEXFVFUDUVTUVPVHUYCUVMUVOVJWKUYDYSYTVF
        VQUEUVQUVNWBXIVFUVMUVOUYOUWAUVOVLUEWHVHUUAVTUUBVTUUCUURUVFUVNWBZUUSUURU
        VEUXCVUBVUTUVEUURUVGVBUXCUURUXDVBVUDABEUVMGHUVANUYRWEYTUUDUUEUURUUSUUFU
        VAUUQGEUUQDUUGUCUUHUUIYTUUJUUK $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp .  The fixed point is unique. $)
      bfplem9 $p |- ( ( ( Z e. X /\ W e. X ) /\
                        ( ( F ` Z ) = Z /\ ( F ` W ) = W ) ) -> Z = W ) $=
        ( wcel wa cfv wceq co cc0 cle wbr clt wn wb cr 0re cme cmsmeti metcl
        mp3an1 eqlelt sylancr adantr metge0 ltnr syl cmul ad2antrr crp rpre
        ax-mp remulcl opreq12 adantl cv wral fveq2 opreq1d opreq1 opreq2d
        breq12d opreq2 rcla42v mpi eqbrtrrd c1 1re w3a ltmul1 mpbii mp3an12
        sylan adantlr cc recn mulid2 3syl breqtrd lelttrd mtand mpbir2and
        eqcomd meteq0 mpbid ) KIUDZHIUDZUEZKDUFZKUGHDUFZHUGUEZUEZKHGUHZUIUGZKHU
        GZXKUIXLXKUIXLUGZUIXLUJUKZUIXLULUKZUMZXGXOXPXRUEUNZXJXGUIUOUDXLUOUDZXSU
        PGUQUDZXEXFXTGPURZKHGIOUSUTZUIXLVAVBVCXGXPXJYAXEXFXPYBKHGIOVDUTVCXKXQXL
        XLULUKZXGYDUMZXJXGXTYEYCXLVEVFVCXKXQUEZXLFXLVGUHZXLXGXTXJXQYCVHZYFFUOUD
        ZXTYGUOUDFVIUDYIUAFVJVKZYHFXLVLVBYHXKXLYGUJUKXQXKXHXIGUHZXLYGUJXJYKXLUG
        XGXHKXIHGVMVNXGYKYGUJUKZXJXGAVOZDUFZBVOZDUFZGUHZFYMYOGUHZVGUHZUJUKZBIVP
        AIVPYLUCYTYLXHYPGUHZFKYOGUHZVGUHZUJUKABKHIIYMKUGZYQUUAYSUUCUJUUDYNXHYPG
        YMKDVQVRUUDYRUUBFVGYMKYOGVSVTWAYOHUGZUUAYKUUCYGUJUUEYPXIXHGYOHDVQVTUUEU
        UBXLFVGYOHKGWBVTWAWCWDVCWEVCYFYGWFXLVGUHZXLULXGXQYGUUFULUKZXJXGXTXQUUGY
        CYIWFUOUDZXTXQUEZUUGYJWGYIUUHUUIWHFWFULUKUUGUBFWFXLWIWJWKWLWMXGUUFXLUGZ
        XJXQXGXTXLWNUDUUJYCXLWOXLWPWQVHWRWSWTXAXBXGXMXNUNZXJYAXEXFUUKYBKHGIOXCU
        TVCXD $.
        $( [2-Sep-2009] $)

      $( Lemma for ~ bfp . ` F ` has exactly one fixed point. $)
      bfplem10 $p |- E! z e. X ( F ` z ) = z $=
        ( vw cv cfv wceq wreu wrex wa wi wral fveq2 id eqeq12d reu4 clm wbr
        bfplem7 wcel bfplem8 ex reximia ax-mp bfplem9 rgen2a mpbir2an ) CUDZEUE
        ZVGUFZCIUGVICIUHZVIUCUDZEUEZVKUFZUIZVGVKUFZUJZUCIUKCIUKVIVMCUCIVOVHVLVG
        VKVGVKEULVOUMUNUOFVGHUPUEUQZCIUHVJABCDEFGHIJKLMNOPQRSTUAUBURVQVICIVGIUS
        ZVQVIABDEFGHIJVGKLMNOPQRSTUAUBUTVAVBVCVPCUCIVRVKIUSUIVNVOABDEFGHVKIJVGK
        LMNOPQRSTUAUBVDVAVEVF $.
        $( [2-Sep-2009] $)
    $}

    ${
      $d M a b c $.  $d K a b c $.  $d F u v $.  $d X u v $.  $d Y u v $.
      bfplem.2a $e |- M e. CMet $.
      bfplem.4a $e |- Y e. X $.
      bfplem.7a $e |- K e. RR+ $.
      bfplem.8a $e |- K < 1 $.
      $( Lemma for ~ bfp .  Apply the deduction theorem. $)
      bfplem11 $p |- ( ( F : X --> X /\ A. x e. X A. y e. X
                            ( ( F ` x ) M ( F ` y ) ) <_ ( K x. ( x M y ) ) )
                                        -> E! z e. X ( F ` z ) = z ) $=
        ( vu vv va vb vc wf cv cfv co cmul cle wbr wral wa wceq wreu csn cxp
        cif fveq1 eqeq1d reubidv wcel copab2 cn cseq1 feq1 fveq2 opreq1d
        opreq1 opreq2d breq12d opreq2 cbvral2v opreq12d breq1d 2ralbidv syl5bb
        anbi12d fconst6 cc0 elisseti fvconst2 opreqan12d cme cmsmeti met0
        mp2an syl6eq cr crp rpre ax-mp a1i rpge0 metcl mp3an1 metge0 mulge0
        syl22anc eqbrtrd rgen2a pm3.2i elimhyp simpli eqid simpri bfplem10
        dedth ) GGDSZATZDUAZBTZDUAZFUBZEXDXFFUBZUCUBZUDUEZBGUFAGUFZUGZCTZDUAZXN
        UHZCGUIXNXMDGHUJZUKZULZUAZXNUHZCGUIDXRDXSUHZXPYACGYBXOXTXNXNDXSUMUNUONO
        CHHXSUAFUBZXSPTZGUPQTGUPUGRTYDXSUAUHUGPQRUQURXQUKUSUBZEFGHPQRIJGGXSSZNT
        ZXSUAZOTZXSUAZFUBZEYGYIFUBZUCUBZUDUEZOGUFNGUFZXMYFYOUGGGXRSZYGXRUAZYIXR
        UAZFUBZYMUDUEZOGUFNGUFZUGDXRYBXCYFXLYOGGDXSUTXLYGDUAZYIDUAZFUBZYMUDUEZO
        GUFNGUFYBYOXKUUEUUBXGFUBZEYGXFFUBZUCUBZUDUEABNOGGXDYGUHZXHUUFXJUUHUDUUI
        XEUUBXGFXDYGDVAVBUUIXIUUGEUCXDYGXFFVCVDVEXFYIUHZUUFUUDUUHYMUDUUJXGUUCUU
        BFXFYIDVAVDUUJUUGYLEUCXFYIYGFVFVDVEVGYBUUEYNNOGGYBUUDYKYMUDYBUUBYHUUCYJ
        FYGDXSUMYIDXSUMVHVIVJVKVLXRXSUHZYPYFUUAYOGGXRXSUTUUKYTYNNOGGUUKYSYKYMUD
        UUKYQYHYRYJFYGXRXSUMYIXRXSUMVHVIVJVLYPUUAGHGKVMYTNOGYGGUPZYIGUPZUGZYSVN
        YMUDUUNYSHHFUBZVNUULUUMYQHYRHFGHYGHGKVOZVPGHYIUUPVPVQFVRUPZHGUPUUOVNUHF
        JVSZKHFGIVTWAWBUUNEWCUPZVNEUDUEZYLWCUPZVNYLUDUEZVNYMUDUEUUSUUNEWDUPZUUS
        LEWEWFWGUUTUUNUVCUUTLEWHWFWGUUQUULUUMUVAUURYGYIFGIWIWJUUQUULUUMUVBUURYG
        YIFGIWKWJEYLWLWMWNWOWPWQZWRKYEWSYCWSLMYFYOUVDWTXAXB $.
        $( [2-Sep-2009] $)
    $}

    $( Banach fixed point theorem, also known as contraction mapping theorem.
       A contraction on a complete metric space has a unique fixed point. $)
    bfp $p |- ( ( ( M e. CMet /\ X =/= (/) ) /\ ( K e. RR+ /\ K < 1 ) /\
                        ( F : X --> X /\ A. x e. X A. y e. X
                          ( ( F ` x ) M ( F ` y ) ) <_ ( K x. ( x M y ) ) ) )
                                        -> E! z e. X ( F ` z ) = z ) $=
      ( vw vf vj vk cms wcel c0 wne wa crp c1 clt wbr wf cv cfv co cmul cle
      wral wceq wreu cdm wi wex neeq1i n0 bitri cop csn cc0 cxp cif c2 cdiv
      dmeq dmeqd feq2d wb feq3 syl bitrd opreq opreq2d breq12d raleqbidv
      anbi12d reueq1 imbi12d opreq1 breq2d 2ralbidv anbi2d imbi1d eqid eleq1
      eleq2d cc 0cn elisseti snnz dmxp ax-mp dmeqi dmsnop eqtr2i cme visset
      xpsn xpeq1i snex cr 0re fconst6 oprvconst2 elsni eqcomd sylan9eq 2thd
      w3a caddc leidi addid1i breqtrri a1i 3adant3 ancoms 3adant2 3adant1
      opreq12d 3brtr4d ismeti eqeltrri cca cn clm wrex snid fconst2 biimpi
      adantl fconst fvconst2 lmbrnns mp3an 1nn met0 mp2an rpgt0 syl5eqbr a1d
      adantr r19.21aiva breq1 ralbidv rcla4ev sylancr mprgbir syl6eqbr breq2
      iscms2i eleqtrri pm3.2i elimhyp simpli simpri 2re 2pos elrpii rpreccl
      halflt1 bfplem11 dedth2h exp31 19.23adv syl5bi imp31 feq23i raleqi
      ralbii anbi12i 3imtr4g 3impia ) FMNZGOPZQZERNZESTUAZQZGGDUBZAUCZDUDZBUCZD
      UDZFUEZEUVSUWAFUEZUFUEZUGUAZBGUHZAGUHZQZCUCZDUDUWJUIZCGUJZUVNUVQQFUKZUKZU
      WNDUBZUWFBUWNUHZAUWNUHZQZUWKCUWNUJZUWIUWLUVLUVMUVQUWRUWSULZUVMIUCZUWNNZIU
      MZUVLUVQUWTULZUVMUWNOPUXCGUWNOHUNIUWNUOUPUVLUXBUXDIUVLUXBUVQUWTUVLUXBQZUV
      QUWTUXEFUXAUXAUQURZUSURZUTZVAZUKZUKZUXKDUBZUVTUWBUXIUEZEUVSUWAUXIUEZUFUEZ
      UGUAZBUXKUHZAUXKUHZQZUWKCUXKUJZULUXLUXMUVQESVBVCUEZVAZUXNUFUEZUGUAZBUXKUH
      AUXKUHZQZUXTULFEUXHUYAFUXIUIZUWRUXSUWSUXTUYGUWOUXLUWQUXRUYGUWOUXKUWNDUBZU
      XLUYGUWNUXKUWNDUYGUWMUXJFUXIVDVEZVFUYGUWNUXKUIZUYHUXLVGUYIUWNUXKUXKDVHVIV
      JUYGUWPUXQAUWNUXKUYIUYGUWFUXPBUWNUXKUYIUYGUWCUXMUWEUXOUGUVTUWBFUXIVKUYGUW
      DUXNEUFUVSUWAFUXIVKVLVMVNVNVOUYGUYJUWSUXTVGUYIUWKCUWNUXKVPVIVQEUYBUIZUXSU
      YFUXTUYKUXRUYEUXLUYKUXPUYDABUXKUXKUYKUXOUYCUXMUGEUYBUXNUFVRVSVTWAWBABCDUY
      BUXIUXKUXAUXKWCUXIMNZUXAUXKNZUXEUYLUYMQUXHMNZUXAUXHUKZUKZNZQFUXHUYGUVLUYL
      UXBUYMFUXIMWDUYGUWNUXKUXAUYIWEVOUXHUXIUIZUYNUYLUYQUYMUXHUXIMWDUYRUYPUXKUX
      AUYRUYOUXJUXHUXIVDVEWEVOUYNUYQCUXHJUXAURZUYPUXFUKZUYSUYOUXFUXGOPUYOUXFUIU
      SUSWFWGWHZWIUXFUXGWJWKWLZUXAUXAWMZWNZUYSUYSUTZUXGUTZUXHWOVUEUXFUXGUXAUXAI
      WPZVUGWQWRABCVUFUYSUXAWSVUEUSWTXAXBUVSUYSNZUWAUYSNZQUVSUWAVUFUEZUSUIZUVSU
      WAUIUYSUYSUSUVSUWAVUAXCZVUHVUIUVSUXAUWAUVSUXAXDVUIUWAUXAUWAUXAXDXEXFXGVUH
      VUIUWJUYSNZXHZUSUSUSXIUEZVUJUWJUVSVUFUEZUWJUWAVUFUEZXIUEUGUSVUOUGUAVUNUSU
      SVUOUGUSXAXJUSWGXKXLXMVUHVUIVUKVUMVULXNVUNVUPUSVUQUSXIVUHVUMVUPUSUIZVUIVU
      MVUHVURUYSUYSUSUWJUVSVUAXCXOXPVUIVUMVUQUSUIZVUHVUMVUIVUSUYSUYSUSUWJUWAVUA
      XCXOXQXRXSXTYAZJUCZUXHYBUDNZYCUYSVVAUBZQZUXAUYSNZVVAUXAUXHYDUDZUAZVVAUWJV
      VFUAZCUYSYEUXAVUGYFZVVDVVAYCUYSUTZUXAVVFVVCVVAVVJUIZVVBVVCVVKYCUXAVVAVUGY
      GYHYIVVJUXAVVFUAZKUCZLUCZUGUAZUXAUXAUXHUEZUVSTUAZULZLYCUHZKYCYEZARUXHWONZ
      VVEYCUYSVVJUBVVLVVTARUHVGVUTVVIYCUXAVUGYJAUXAUXHUXAKLVVJUYSVUDVVNYCNZVVNV
      VJUDUXAYCUXAVVNVUGYKXEYLYMUVSRNZSYCNSVVNUGUAZVVQULZLYCUHZVVTYNVWCVWELYCVW
      CVWEVWBVWCVVQVWDVWCVVPUSUVSTVWAVVEVVPUSUIVUTVVIUXAUXHUYSVUDYOYPUVSYQYRYSY
      TUUAVVSVWFKSYCVVMSUIZVVRVWELYCVWGVVOVWDVVQVVMSVVNUGUUBWBUUCUUDUUEUUFUUGVV
      HVVGCUXAUYSUWJUXAVVAVVFUUHUUDUUEUUIUXAUYTUYPUXAUYSUYTVVIVUCUUJVUBUUJUUKUU
      LZUUMUYLUYMVWHUUNUYBRNZUYBSTUAZUVQVWIVWJQUYARNZUYASTUAZQEUYAUYKUVOVWIUVPV
      WJEUYBRWDEUYBSTUUBVOUYAUYBUIVWKVWIVWLVWJUYAUYBRWDUYAUYBSTUUBVOVWKVWLVBRNV
      WKVBUUOUUPUUQVBUURWKUUSUUKUULZUUMVWIVWJVWMUUNUUTUVAUVBUVCUVDUVEUVRUWOUWHU
      WQGGUWNUWNDHHUVFUWHUWGAUWNUHUWQUWGAGUWNHUVGUWGUWPAUWNUWFBGUWNHUVGUVHUPUVI
      GUWNUIUWLUWSVGHUWKCGUWNVPWKUVJUVK $.
      $( [2-Sep-2009] $)
  $}

