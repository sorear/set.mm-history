$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d ph x y z $.  $d G x y z $.  $d H x y z $.  $d U x y $.
    isringd.1 $e |- G = ( 1st ` R ) $.
    isringd.2 $e |- H = ( 2nd ` R ) $.
    isringd.3 $e |- ( ph -> R e. ( _V X. _V ) ) $.
    isringd.4 $e |- ( ph -> G e. AbelOp ) $.
    isringd.5 $e |- ( ph -> X = ran G ) $.
    isringd.6 $e |- ( ph -> H : ( X X. X ) --> X ) $.
    isringd.7 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                                  -> ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $.
    isringd.8 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $.
    isringd.9 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) $.
    isringd.10 $e |- ( ph -> U e. X ) $.
    isringd.11 $e |- ( ( ph /\ y e. X ) -> ( y H U ) = y ) $.
    isringd.12 $e |- ( ( ph /\ y e. X ) -> ( U H y ) = y ) $.
    $( Conditions that determine a ring. $)
    isringd $p |- ( ph -> R e. RingOps ) $=
      ( cop cring c1st cfv c2nd cvv cxp wrel wcel wceq 1st2nd relxp sylancr
      opeq12i syl6eqr cabl crn wf wa cv co w3a wral wrex xpeq12 syl11anc feq2d
      wb feq3 syl bitrd mpbid wi eleq2d 3anbi123d 3jca ex sylbird 3expd imp3a
      r19.21adv r19.21aivv eleqtrd jca r19.21aiv opreq2 eqeq1d opreq1 anbi12d
      ralbidv rcla4ev jca31 fvex eqeltri eqid isringo ax-mp sylibr eqeltrd ) AE
      GHUBZUCAEEUDUEZEUFUEZUBZXAUGUGUHZUIEXEUJEXDUKAEXEULUGUGUMLUNGXBHXCJKUOUPA
      GUQUJZGURZXGUHZXGHUSZUTBVAZCVAZHVBZDVAZHVBXJXKXMHVBZHVBUKZXJXKXMGVBHVBXLX
      JXMHVBZGVBUKZXJXKGVBXMHVBXPXNGVBUKZVCZDXGVDZCXGVDBXGVDZXKXJHVBZXKUKZXLXKU
      KZUTZCXGVDZBXGVEZUTZUTZXAUCUJZAXFXIYHMAIIUHZIHUSZXIOAYLXHIHUSZXIAYKXHIHAI
      XGUKZYNYKXHUKNNIXGIXGVFVGVHAYNYMXIVINIXGXHHVJVKVLVMAYAYGAXTBCXGXGAXJXGUJZ
      XKXGUJZUTXSDXGAYOYPXMXGUJZXSVNAYOYPYQXSAYOYPYQVCXJIUJZXKIUJZXMIUJZVCZXSAY
      RYOYSYPYTYQAIXGXJNVOAIXGXKNVOZAIXGXMNVOVPAUUAXSAUUAUTXOXQXRPQRVQVRVSVTWAW
      BWCAFXGUJXKFHVBZXKUKZFXKHVBZXKUKZUTZCXGVDZYGAFIXGSNWDAUUGCXGAYPYSUUGUUBAY
      SUUGAYSUTUUDUUFTUAWEVRVSWFYFUUHBFXGXJFUKZYEUUGCXGUUIYCUUDYDUUFUUIYBUUCXKX
      JFXKHWGWHUUIXLUUEXKXJFXKHWIWHWJWKWLVGWEWMHUGUJYJYIVIHXCUGKEUFWNWOBCDUGGHX
      GXGWPWQWRWSWT $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringnegcl.1 $e |- G = ( 1st ` R ) $.
    ringnegcl.2 $e |- X = ran G $.
    ringnegcl.3 $e |- N = ( inv ` G ) $.
    $( A ring is closed under negation. $)
    ringnegcl $p |- ( ( R e. RingOps /\ A e. X ) -> ( N ` A ) e. X ) $=
      ( cgr wcel cfv cring grpoinvcl ringogrpo sylan ) CIJAEJADKEJBLJACDEGHMBCF
      NO $.
      $( [10-Jun-2010] $)

    ${
      ringaddneg.4 $e |- Z = ( Id ` G ) $.
      $( Adding the negative in a ring gives zero. $)
      ringaddneg1 $p |- ( ( R e. RingOps /\ A e. X )
            -> ( A G ( N ` A ) ) = Z ) $=
        ( cgr wcel cfv co wceq cring grporinv ringogrpo sylan ) CKLAELAADMCNFOB
        PLAFCDEHJIQBCGRS $.
        $( [10-Jun-2010] $)

      $( Adding the negative in a ring gives zero. $)
      ringaddneg2 $p |- ( ( R e. RingOps /\ A e. X )
             -> ( ( N ` A ) G A ) = Z ) $=
        ( cgr wcel cfv co wceq cring grpolinv ringogrpo sylan ) CKLAELADMACNFOB
        PLAFCDEHJIQBCGRS $.
        $( [10-Jun-2010] $)
    $}

    ringsub.4 $e |- D = ( /g ` G ) $.
    $( Subtraction in a ring, in terms of addition and negation. $)
    ringsub $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                        -> ( A D B ) = ( A G ( N ` B ) ) ) $=
      ( cgr wcel co cfv wceq cring grpdivval ringogrpo syl3an1 ) ELMAGMBGMABCNA
      BFOENPDQMABCEFGIJKRDEHST $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringneg.1 $e |- G = ( 1st ` R ) $.
    ringneg.2 $e |- H = ( 2nd ` R ) $.
    ringneg.3 $e |- X = ran G $.
    ringneg.4 $e |- N = ( inv ` G ) $.
    ringneg.5 $e |- U = ( Id ` H ) $.
    $( Negation in a ring is the same as left multiplication by ` -u 1 ` . $)
    ringnegmn1l $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( ( N ` U ) H A ) ) $=
      ( cring wcel wa cfv co wceq cgi crn c1st rneqi eqtri ring1cl ringnegcl
      mpdan jca adantr ringodir 3exp2 imp42 an32s eqid ringaddneg1 opreq1d
      ringolz eqtrd ringolidm 3eqtr3rd wb ringocl 3expa cgr grpoinvid1
      ringogrpo syl3an1 mpd3an3 mpbird ) BMNZAGNZOZAFPCFPZAEQZRZAVMDQZDSPZRZVKC
      VLDQZAEQZCAEQZVMDQZVPVOVKCGNZVLGNZOZVSWARZVIWDVJVIWBWCBCEGGDTBUAPZTJDWFHU
      BUCZILUDZVIWBWCWHCBDFGHJKUEUFZUGUHVIWDVJWEVIWBWCVJWEVIWBWCVJWECVLABDEGHIJ
      UIUJUKULUFVKVSVPAEQVPVKVRVPAEVIVRVPRZVJVIWBWJWHCBDFGVPHJKVPUMZUNUFUHUOABD
      EGVPWKJHIUPUQVKVTAVMDABCEGIWGLURUOUSVIVJVMGNZVNVQUTZVKWCWLVIWCVJWIUHVIWCV
      JWLVIWCVJWLVLABDEGHIJVAVBULUFDVCNVJWLWMVIAVMVPDFGJWKKVDBDHVEVFVGVH $.
      $( [10-Jun-2010] $)

    $( Negation in a ring is the same as right multiplication by ` -u 1 ` . $)
    ringnegmn1r $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( A H ( N ` U ) ) ) $=
      ( cring wcel wa cfv co wceq cgi crn c1st rneqi eqtri ring1cl ringnegcl
      mpdan adantr jca ringodi 3exp2 imp43 eqid ringaddneg2 opreq2d ringorz
      eqtrd ringoridm 3eqtr3rd wb ringocl mpd3an3 cgr grpoinvid2 ringogrpo
      syl3an1 mpbird ) BMNZAGNZOZAFPACFPZEQZRZVKADQZDSPZRZVIAVJCDQZEQZVKACEQZDQ
      ZVNVMVIVJGNZCGNZOVQVSRZVIVTWAVGVTVHVGWAVTBCEGGDTBUAPZTJDWCHUBUCZILUDZCBDF
      GHJKUEUFUGZVGWAVHWEUGUHVGVHVTWAWBVGVHVTWAWBAVJCBDEGHIJUIUJUKUFVIVQAVNEQVN
      VIVPVNAEVGVPVNRZVHVGWAWGWECBDFGVNHJKVNULZUMUFUGUNABDEGVNWHJHIUOUPVIVRAVKD
      ABCEGIWDLUQUNURVGVHVKGNZVLVOUSZVGVHVTWIWFAVJBDEGHIJUTVADVBNVHWIWJVGAVKVND
      FGJWHKVCBDHVDVEVAVF $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringnegmul.1 $e |- G = ( 1st ` R ) $.
    ringnegmul.2 $e |- H = ( 2nd ` R ) $.
    ringnegmul.3 $e |- X = ran G $.
    ringnegmul.4 $e |- N = ( inv ` G ) $.
    $( Negation of a product in a ring. $)
    ringneglmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( ( N ` A ) H B ) ) $=
      ( cring wcel w3a cgi cfv co wceq wi crn c1st rneqi eqtri eqid ring1cl
      ringnegcl mpdan ringoass 3exp2 mpd 3imp ringnegmn1l 3adant3 opreq1d wa
      ringocl 3expb syldan 3impb 3eqtr4rd ) CLMZAGMZBGMZNZEOPZFPZAEQZBEQZVFABEQ
      ZEQZAFPZBEQVIFPZVAVBVCVHVJRZVAVFGMZVBVCVMSSVAVEGMVNCVEEGGDTCUAPZTJDVOHUBU
      CIVEUDZUEVECDFGHJKUFUGVAVNVBVCVMVFABCDEGHIJUHUIUJUKVDVKVGBEVAVBVKVGRVCACV
      EDEFGHIJKVPULUMUNVAVBVCVLVJRZVAVBVCUOVIGMZVQVAVBVCVRABCDEGHIJUPUQVICVEDEF
      GHIJKVPULURUSUT $.
      $( [19-Jun-2010] $)

    $( Negation of a product in a ring. $)
    ringnegrmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( A H ( N ` B ) ) ) $=
      ( cring wcel w3a co cgi cfv wceq wi crn c1st rneqi eqtri eqid ring1cl
      ringnegcl mpdan ringoass 3exp2 com24 com34 mpd 3imp wa ringocl 3expb
      ringnegmn1r syldan 3impb 3adant2 opreq2d 3eqtr4d ) CLMZAGMZBGMZNZABEOZEPQ
      ZFQZEOZABVIEOZEOZVGFQZABFQZEOVCVDVEVJVLRZVCVIGMZVDVEVOSSVCVHGMVPCVHEGGDTC
      UAQZTJDVQHUBUCIVHUDZUEVHCDFGHJKUFUGVCVPVEVDVOVCVDVEVPVOVCVDVEVPVOABVICDEG
      HIJUHUIUJUKULUMVCVDVEVMVJRZVCVDVEUNVGGMZVSVCVDVEVTABCDEGHIJUOUPVGCVHDEFGH
      IJKVRUQURUSVFVNVKAEVCVEVNVKRVDBCVHDEFGHIJKVRUQUTVAVB $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringsubdi.1 $e |- G = ( 1st ` R ) $.
    ringsubdi.2 $e |- H = ( 2nd ` R ) $.
    ringsubdi.3 $e |- X = ran G $.
    ringsubdi.4 $e |- D = ( /g ` G ) $.
    $( Ring multiplication distributes over subtraction. $)
    ringsubdi $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( A H ( B D C ) ) = ( ( A H B ) D ( A H C ) ) ) $=
      ( cring wcel w3a wa cgn cfv co wceq idd eqid ringnegcl ex 3anim123d imp
      ringodi syldan ringnegrmul 3adant3r2 opreq2d eqtr4d ringsub 3adant3r1
      ringocl 3adant3r3 jca 3expb 3eqtr4d ) EMNZAHNZBHNZCHNZOZPZABCFQRZRZFSZGSZ
      ABGSZACGSZVFRZFSZABCDSZGSVJVKDSZVEVIVJAVGGSZFSZVMUTVDVAVBVGHNZOZVIVQTUTVD
      VSUTVAVAVBVBVCVRUTVAUAUTVBUAUTVCVRCEFVFHIKVFUBZUCUDUEUFABVGEFGHIJKUGUHVEV
      LVPVJFUTVAVCVLVPTVBACEFGVFHIJKVTUIUJUKULVEVNVHAGUTVBVCVNVHTVABCDEFVFHIKVT
      LUMUNUKUTVDVJHNZVKHNZPVOVMTZVEWAWBUTVAVBWAVCABEFGHIJKUOUPUTVAVCWBVBACEFGH
      IJKUOUJUQUTWAWBWCVJVKDEFVFHIKVTLUMURUHUS $.
      $( [19-Jun-2010] $)

    $( Ring multiplication distributes over subtraction. $)
    ringsubdir $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( ( A D B ) H C ) = ( ( A H C ) D ( B H C ) ) ) $=
      ( cring wcel w3a wa cgn cfv co wceq idd eqid ringnegcl ex 3anim123d imp
      ringodir syldan ringneglmul 3adant3r1 opreq2d eqtr4d ringsub 3adant3r3
      opreq1d ringocl 3adant3r2 jca 3expb 3eqtr4d ) EMNZAHNZBHNZCHNZOZPZABFQRZR
      ZFSZCGSZACGSZBCGSZVGRZFSZABDSZCGSVKVLDSZVFVJVKVHCGSZFSZVNVAVEVBVHHNZVDOZV
      JVRTVAVEVTVAVBVBVCVSVDVDVAVBUAVAVCVSBEFVGHIKVGUBZUCUDVAVDUAUEUFAVHCEFGHIJ
      KUGUHVFVMVQVKFVAVCVDVMVQTVBBCEFGVGHIJKWAUIUJUKULVFVOVICGVAVBVCVOVITVDABDE
      FVGHIKWALUMUNUOVAVEVKHNZVLHNZPVPVNTZVFWBWCVAVBVDWBVCACEFGHIJKUPUQVAVCVDWC
      VBBCEFGHIJKUPUJURVAWBWCWDVKVLDEFVGHIKWALUMUSUHUT $.
      $( [19-Jun-2010] $)
  $}

  ${
    zerdivempx.1 $e |- G = ( 1st ` R ) $.
    zerdivempx.2 $e |- H = ( 2nd ` R ) $.
    zerdivempx.3 $e |- Z = ( Id ` G ) $.
    zerdivempx.4 $e |- X = ran G $.
    zerdivempx.5 $e |- U = ( Id ` H ) $.
    $d A a $.  $d B a $.  $d H a $.  $d R a $.  $d X a $.  $d Z a $.
    $( In a unitary ring a left invertible element is not a zero divisor.
       Generalization of ~ zerdivemp1 by Frederic Line. $)
    zerdivemp1x $p |- ( ( R e. RingOps /\ A e. X /\ E. a e. X ( a H A ) = U )
       -> ( B e. X -> ( ( A H B ) = Z -> B = Z ) ) ) $=
      ( cring wcel cv co wceq wrex wi w3a wa simpl1 simpr1 simpr3 simpl3 3jca
      jca ringoass eqtr ex opreq1 ringorz 3adant3 crn c1st cfv rneqi eqtri
      ringolidm 3adant2 simp1 simp2 simp3 3eqtr3d a1d 3exp com14 com13 sylc
      com15 com24 syl eqcoms com25 com12 3imp syl6 3imp1 3syl pm2.43i 3exp1
      opreq2 syl5com r19.23aiv ) COPZAGPZIQZAFRZDSZIGTZBGPZABFRZHSZBHSZUAUAZWLW
      HWGWQWKWHWGWQUAZUAIGWIGPZWKWHWRWOWGWMWSWKWHUBZWPWGWIWNFRZWIHFRZSZWMWTWPUA
      UAWOWGXCWMWTWPWGXCWMUBZWTUCZWPXEWGWSWHWMUBZUCWJBFRZXASZXEWPUAXEWGXFWGXCWM
      WTUDXEWSWHWMXDWSWKWHUEXDWSWKWHUFWGXCWMWTUGUHUIWIABCEFGJKMUJXEXHWPWGXCWMWT
      XHWPUAXHXCWMWTWGWPXHXCXGXBSZWMWTWGWPUAZUAUAXHXCXIXGXAXBUKULWTWMXIXJWSWKWH
      WMXIXJUAUAZWKWSWHXKUAZWKXGDBFRZSZWSXLUAWJDBFUMXNXIWHWMWSXJXIWHWMWSXJUAUAU
      AZUAXMXGXMXGSZXIXOXPXIUCXMXBSZXOXMXGXBUKXQWSWMWHXJWGWSWMWHXQWPWGWSWMWHXQW
      PUAZUAZWGWSWMUBXBHSZXMBSZXSWGWSXTWMWICEFGHLMJKUNUOWGWMYAWSBCDFGKGEUPCUQUR
      ZUPMEYBJUSUTNVAVBWHYAXTXRXQYAXTWHWPXQYAXTWHWPUAXQYAXTUBZWPWHYCXMXBBHXQYAX
      TVCXQYAXTVDXQYAXTVEVFVGVHVIVJVKVHVLVMVNULVOVPVNVQVRVJVSVLVTVQWAWBWCWNHWIF
      WDWEVIVHWFVJVR $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d g h $.
    isdivrng1.1 $e |- G = ( 1st ` R ) $.
    isdivrng1.2 $e |- H = ( 2nd ` R ) $.
    isdivrng1.3 $e |- Z = ( Id ` G ) $.
    isdivrng1.4 $e |- X = ran G $.
    $( The predicate "is a division ring". $)
    isdivrng1 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\
             ( H |` ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. GrpOp ) ) $=
      ( vg vh cdrng wcel c1st cfv c2nd cop wceq cring csn cdif cxp cres cgr wa
      wrel cv crn cgi copab relopab df-drngo releqi mpbir 1st2nd mpan relrng
      adantr wb opeq12i eqeq2i cvv fvex eqeltri isdivrngo ax-mp sneqi difeq12i
      xpeq12i reseq2i eleq1i anbi2i bitr4i eleq1 anbi1d bibi12d mpbiri sylbir
      pm5.21nii ) ALMZAANOZAPOZQZRZASMZCDETZUAZWGUBZUCZUDMZUEZLUFZVTWDWLJUGZKUG
      ZQSMWNWMUHWMUIOTUAZWOUBUCUDMUEZJKUJZUFWPJKUKLWQJKULUMUNALUOUPWEWDWJSUFWEW
      DUQASUOUPURWDABCQZRZVTWKUSZWRWCABWACWBFGUTVAWSWTWRLMZWRSMZWJUEZUSXAXBCBUH
      ZBUIOZTZUAZXGUBZUCZUDMZUEZXCCVBMXAXKUSCWBVBGAPVCVDVBBCVEVFWJXJXBWIXIUDWHX
      HCWGXGWGXGDXDWFXFIEXEHVGVHZXLVIVJVKVLVMWSVTXAWKXCAWRLVNWSWEXBWJAWRSVNVOVP
      VQVRVS $.
      $( [8-Jun-2010] $)

    $( The product of two nonzero elements of a division ring is nonzero. $)
    divrngcl $p |- ( ( R e. DivRingOps /\ A e. ( X \ { Z } )
                    /\ B e. ( X \ { Z } ) ) -> ( A H B ) e. ( X \ { Z } ) ) $=
      ( cring wcel csn cdif cxp cres cgr wa co cdrng wceq oprvres adantl crn
      wi eqid grpocl 3expib cdm grprndm wss difss xpss12 mp2an wf ringsm fdm
      syl sseq2d mpbiri ssdmres sylib adantr dmeqd dmxpid syl6eq eqtrd eleq2d
      anbi12d 3imtr3d imp eqeltrrd 3impb isdivrng1 syl3an1b ) CLMZEFGNZOZVSPZQZ
      RMZSZAVSMZBVSMZABETZVSMZCUAMWCWDWEWGWCWDWESZSABWATZWFVSWHWIWFUBWCABVSVSEU
      CUDWCWHWIVSMZWCAWAUEZMZBWKMZSZWIWKMZWHWJWBWNWOUFVQWBWLWMWOABWAWKWKUGUHUIU
      DWCWLWDWMWEWCWKVSAWCWKWAUJZUJZVSWBWKWQUBVQWAUKUDWCWQVTUJVSWCWPVTVQWPVTUBZ
      WBVQVTEUJZULZWRVQWTVTFFPZULZVSFULZXCXBFVRUMZXDVSFVSFUNUOVQWSXAVTVQXAFEUPW
      SXAUBCDEFHIKUQXAFEURUSUTVAVTEVBVCVDVEVSVFVGVHZVIWCWKVSBXEVIVJWCWKVSWIXEVI
      VKVLVMVNCDEFGHIJKVOVP $.
      $( [9-Jun-2010] $)

    $d H x y u v w z $.  $d X x y u v w z $.  $d Z x y u v w z $.
    $d R x y u v w z $.  $d U x y u v w z $.
    isdivrng2.5 $e |- U = ( Id ` H ) $.
    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible. $)
    isdivrng2 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
             A. x e. ( X \ { Z } ) E. y e. ( X \ { Z } ) ( y H x ) = U ) ) ) $=
      ( vu vv vw vz cdrng wcel cring csn cdif cxp cres cgr wa wne cv co wceq
      wrex wral isdivrng1 dvrunz sylbir crn cdm grprndm adantl wss difss
      xpss12 mp2an wf ringsm fdm syl sseq2d mpbiri adantr ssdmres sylib dmeqd
      dmxpid a1i 3eqtrd eleq2d biimpar cgn cfv eqid grpoinvcl adantll cgi
      grpolinv cmagm cexid cin cmnd unmnd mndmgmid sseqtri rnplrnml mpbii
      eldifsn c1st rneqi eqtri ring1cl sylanbrc grpmnd mndismgm exidresid
      syl31anc eqtrd opreq1 eqeq1d rcla4ev syl11anc syldan rexeqdv wb oprvres
      ancoms rexbidva bitrd mpbid r19.21aiva jca cvv fvex eqeltri rnex difexg
      ax-mp wfn ffnoprv fnssres ffn sylancl ringocl 3expb eldifi anim12i
      sylan2 adantlr wi zerdivemp1x ssrexv syl3an3 syl3an2 imp necon3d impr
      sylan2b an32s ancom2s opreq2 rexbidv rcla4v imdistanri an42s adantlrl
      eqeltrd r19.21aivva w3a ringoass 3anim123i 3adant3 opreq1d 3adantr3
      simpr3 simpr1 foprrn 3adant3r1 sylan 3adant1 opreq2d 3eqtr4d anim1i
      sylibr adantrr ringolidm adantlrr rcla4va cbvrexv isgrpda impbida
      pm5.32i bitri ) CRSZCTSZFGHUAZUBZUWDUCZUDZUESZUFZUWBDHUGZBUHZAUHZFUIZDUJZ
      BUWDUKZAUWDULZUFZUFZCEFGHIJKLUMZUWBUWGUWPUWBUWGUWPUWHUWIUWOUWHUWAUWIUWRCD
      EFGHIJLKMUNUOZUWHUWNAUWDUWHUWKUWDSZUFZUWJUWKUWFUIZDUJZBUWFUPZUKZUWNUWHUWT
      UWKUXDSZUXEUWHUXFUWTUWHUXDUWDUWKUWHUXDUWFUQZUQZUWEUQZUWDUWGUXDUXHUJUWBUWF
      URUSUWHUXGUWEUWHUWEFUQZUTZUXGUWEUJUWBUXKUWGUWBUXKUWEGGUCZUTZUWDGUTZUXNUXM
      GUWCVAZUXOUWDGUWDGVBVCZUWBUXJUXLUWEUWBUXLGFVDZUXJUXLUJCEFGIJLVEZUXLGFVFVG
      VHVIVJUWEFVKVLVMUXIUWDUJUWHUWDVNVOVPZVQVRUWHUXFUFZUWKUWFVSVTZVTZUXDSZUYBU
      WKUWFUIZDUJZUXEUWGUXFUYCUWBUWKUWFUYAUXDUXDWAZUYAWAZWBWCUXTUYDUWFWDVTZDUWG
      UXFUYDUYHUJUWBUWKUYHUWFUYAUXDUYFUYHWAUYGWEWCUWHUYHDUJZUXFUWHFWFWGWHSZUWDF
      UPZUTZDUWDSZUWFWFSZUYIUWBUYJUWGUWBFWISUYJCFJWJFWKVGVJUWBUYLUWGUWBUWDEUPZU
      TUYLUWDGUYOUXOLWLUWBUYOUYKUWDCEFJIWMVHWNVJUYMDGSZUWIUWHDGHWOZUWBUYPUWGCDF
      GGUYOCWPVTZUPLEUYRIWQWRZJMWSZVJUWSWTUWGUYNUWBUWGUWFWISUYNUWFXAUWFXBVGUSDF
      UWFUYKUWDUYKWAMUWFWAXCXDVJXEUXCUYEBUYBUXDUWJUYBUJUXBUYDDUWJUYBUWKUWFXFXGX
      HXIXJUXAUXEUXCBUWDUKZUWNUXAUXCBUXDUWDUWHUXDUWDUJUWTUXSVJXKUWTVUAUWNXLUWHU
      WTUXCUWMBUWDUWTUWJUWDSZUFUXBUWLDVUBUWTUXBUWLUJUWJUWKUWDUWDFXMXNXGXOUSXPXQ
      XRXSUWQNOPDQUWFUWDUWDXTSZUWQGXTSVUCGUYOXTLEEUYRXTICWPYAYBYCYBGUWCXTYDYEVO
      UWEUWDUWFVDZUWFUWEYFZNUHZOUHZUWFUIZUWDSZOUWDULNUWDULUWQNOUWDUWDUWDUWFYGFU
      XLYFZUXMVUEUWQUXLUWEFYHUWBVUJUWPUWBUXQVUJUXRUXLGFYIVGVJUXPYJUWQVUINOUWDUW
      DUWQVUFUWDSZVUGUWDSZUFZUFZVUHVUFVUGFUIZUWDVUMVUHVUOUJZUWQVUFVUGUWDUWDFXMZ
      USVUOUWDSZVUOGSZVUOHUGZVUNVUOGHWOUWBVUMVUSUWPUWBVUFGSZVUGGSZUFVUSVUMUWBVV
      AVVBVUSVUFVUGCEFGIJLYKYLVUKVVAVULVVBVUFGUWCYMZVUGGUWCYMZYNYOYPUWBUWOVUMVU
      TUWIUWBVULUWOVUKVUTUWBVULUFZUWJVUFFUIZDUJZBUWDUKZVUKUFVUTUWOVUKUFVVEVUKVV
      HVUTUWBVUKVVHUFZVULVUTUWBVVIUFZVVBVUGHUGZUFVUTVULVVJVVBVVKVUTVVJVVBUFVUOH
      VUGHVVJVVBVUOHUJVUGHUJYQZUWBVUKVVHVVBVVLYQZUWBVVAVVHVVMVUKUWBVVAVVGBGUKZV
      VMVVHVUFVUGCDEFGHBIJKLMYRUXNVVHVVNYQUXOVVGBUWDGYSYEYTVVCUUAYLUUBUUCUUDVUG
      GHWOUUEUUFUUGVUKUWOVVHUWNVVHAVUFUWDUWKVUFUJZUWMVVGBUWDVVOUWLVVFDUWKVUFUWJ
      FUUHXGUUIZUUJUUKYOUULUUMWTZUUNUUOWTZUWQVUKVULPUHZUWDSZUUPZUFZVUOVVSFUIZVU
      FVUGVVSFUIZFUIZVUHVVSUWFUIZVUFVUGVVSUWFUIZUWFUIZUWBVWAVWCVWEUJZUWPUWBVVAV
      VBVVSGSZUUPVWIVWAVUFVUGVVSCEFGIJLUUQVUKVVAVULVVBVVTVWJVVCVVDVVSGUWCYMUURY
      OYPVWBVWFVUOVVSUWFUIZVWCVWBVUHVUOVVSUWFVWAVUPUWQVUKVULVUPVVTVUQUUSUSUUTVW
      BVURVVTVWKVWCUJUWQVUKVULVURVVTVVQUVAUWQVUKVULVVTUVBVUOVVSUWDUWDFXMXIXEVWB
      VWHVUFVWGFUIZVWEVWBVUKVWGUWDSZVWHVWLUJUWQVUKVULVVTUVCVUDVWAVWMUWQVUDVULVV
      TVWMVUKVUGVVSUWDUWDUWDUWFUVDUVEVVRUVFVUFVWGUWDUWDFXMXIVWBVWGVWDVUFFVWAVWG
      VWDUJZUWQVULVVTVWNVUKVUGVVSUWDUWDFXMUVGUSUVHXEUVIUWBUWIUYMUWOUWBUWIUFZUYP
      UWIUFUYMUWBUYPUWIUYTUVJUYQUVKZUVLUWBUWIVUKDVUFUWFUIZVUFUJUWOVWOVUKUFVWQDV
      UFFUIZVUFUYMVUKVWQVWRUJVWODVUFUWDUWDFXMVWPUVFUWBVUKVWRVUFUJZUWIUWBVVAVWSV
      UKVUFCDFGJUYSMUVMVVCYOYPXEUVNUWBUWOVUKQUHZVUFUWFUIZDUJZQUWDUKZUWIUWOVUKVX
      CUWBVUKUWOVXCVUKUWOVVHVXCUWNVVHAVUFUWDVVPUVOVUKVWTVUFFUIZDUJZQUWDUKZVXCVV
      HVUKVXCVXFVUKVXBVXEQUWDVWTUWDSZVUKVXBVXEXLVXGVUKUFVXAVXDDVWTVUFUWDUWDFXMX
      GXNXOVRVVGVXEBQUWDUWJVWTUJVVFVXDDUWJVWTVUFFXFXGUVPUUEXJXNWCUUMUVQUVRUVSUV
      T $.
      $( [8-Jun-2010] $)

    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible. $)
    isdivrng3 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
                        A. x e. ( X \ { Z } ) E. y e. X ( y H x ) = U ) ) ) $=
      ( cdrng wcel cring wne cv co wceq csn cdif wrex wral wa isdivrng2 wb wss
      wi difss ssrexv ax-mp pm3.2 opreq1 eqeq1d ringolz syl5cbir necon3d imp
      neeq1 biimparc sylan2 an4s anassrs syl5com eldifsn syl6ibr imdistanda
      ancom 3imtr4g reximdv2 impbid2 eldifi ralbidva pm5.32da pm5.32i bitri )
      CNOCPOZDHQZBRZARZFSZDTZBGHUAZUBZUCZAWEUDZUEZUEVRVSWCBGUCZAWEUDZUEZUEABCDE
      FGHIJKLMUFVRWHWKVRVSWGWJVRVSUEZWFWIAWEWLWAGOZWFWIUGWAWEOWLWMUEZWFWIWEGUHW
      FWIUIGWDUJWCBWEGUKULWNWCWCBGWEWNWCVTGOZUEWCVTWEOZUEWOWCUEWPWCUEWNWCWOWPWN
      WCUEZWOWOVTHQZUEZWPWOWRWSWQWOWRUMWLWMWCWRVRWMVSWCWRVRWMUEZWBHQZWRVSWCUEWT
      XAWRWTVTHWBHVTHTZWBHTHWAFSZHTWTXBWBXCHVTHWAFUNUOWACEFGHKLIJUPUQURUSWCXAVS
      WBDHUTVAVBVCVDVEVTGHVFVGVHWOWCVIWPWCVIVJVKVLWAGWDVMVBVNVOVPVQ $.
      $( [10-Jun-2010] $)
  $}

  $( A field is a division ring. $)
  flddivrng $p |- ( K e. Fld -> K e. DivRingOps ) $=
    ( cfld wcel cdrng ccm2 cin df-fld eleq2i inss1 sseli sylbi ) ABCADEFZCADCBL
    AGHLDADEIJK $.
    $( [10-Jun-2010] $)

