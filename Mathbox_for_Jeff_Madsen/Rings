$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ringnegcl.1 $e |- G = ( 1st ` R ) $.
    ringnegcl.2 $e |- X = ran G $.
    ringnegcl.3 $e |- N = ( inv ` G ) $.
    $( A ring is closed under negation.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    ringnegcl $p |- ( ( R e. RingOps /\ A e. X ) -> ( N ` A ) e. X ) $=
      ( cring wcel cgr cfv ringogrpo grpoinvcl sylan ) BIJCKJAEJADLEJBCFMACDEGH
      NO $.
      $( [10-Jun-2010] $)

    ${
      ringaddneg.4 $e |- Z = ( Id ` G ) $.
      $( Adding the negative in a ring gives zero.  (Contributed by Jeff
         Madsen, 10-Jun-2010.) $)
      ringaddneg1 $p |- ( ( R e. RingOps /\ A e. X )
            -> ( A G ( N ` A ) ) = Z ) $=
        ( cring wcel cgr cfv co wceq ringogrpo grporinv sylan ) BKLCMLAELAADNCO
        FPBCGQAFCDEHJIRS $.
        $( [10-Jun-2010] $)

      $( Adding the negative in a ring gives zero.  (Contributed by Jeff
         Madsen, 10-Jun-2010.) $)
      ringaddneg2 $p |- ( ( R e. RingOps /\ A e. X )
             -> ( ( N ` A ) G A ) = Z ) $=
        ( cring wcel cgr cfv co wceq ringogrpo grpolinv sylan ) BKLCMLAELADNACO
        FPBCGQAFCDEHJIRS $.
        $( [10-Jun-2010] $)
    $}

    ringsub.4 $e |- D = ( /g ` G ) $.
    $( Subtraction in a ring, in terms of addition and negation.  (Contributed
       by Jeff Madsen, 19-Jun-2010.) $)
    ringsub $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                        -> ( A D B ) = ( A G ( N ` B ) ) ) $=
      ( cring wcel cgr co cfv wceq ringogrpo grpodivval syl3an1 ) DLMENMAGMBGMA
      BCOABFPEOQDEHRABCEFGIJKST $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringneg.1 $e |- G = ( 1st ` R ) $.
    ringneg.2 $e |- H = ( 2nd ` R ) $.
    ringneg.3 $e |- X = ran G $.
    ringneg.4 $e |- N = ( inv ` G ) $.
    ringneg.5 $e |- U = ( Id ` H ) $.
    $( Negation in a ring is the same as left multiplication by ` -u 1 ` .
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    ringnegmn1l $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( ( N ` U ) H A ) ) $=
      ( wcel wa cfv co wceq crn mpdan adantr cring cgi c1st rneqi eqtri ring1cl
      ringnegcl jca ringodir 3exp2 imp42 an32s eqid ringaddneg1 opreq1d ringolz
      eqtrd ringolidm 3eqtr3rd ringocl cgr ringogrpo grpoinvid1 syl3an1 mpd3an3
      wb 3expa mpbird ) BUAMZAGMZNZAFOCFOZAEPZQZAVMDPZDUBOZQZVKCVLDPZAEPZCAEPZV
      MDPZVPVOVKCGMZVLGMZNZVSWAQZVIWDVJVIWBWCBCEGGDRBUCOZRJDWFHUDUEZILUFZVIWBWC
      WHCBDFGHJKUGSZUHTVIWDVJWEVIWBWCVJWEVIWBWCVJWECVLABDEGHIJUIUJUKULSVKVSVPAE
      PVPVKVRVPAEVIVRVPQZVJVIWBWJWHCBDFGVPHJKVPUMZUNSTUOABDEGVPWKJHIUPUQVKVTAVM
      DABCEGIWGLURUOUSVIVJVMGMZVNVQVFZVKWCWLVIWCVJWITVIWCVJWLVIWCVJWLVLABDEGHIJ
      UTVGULSVIDVAMVJWLWMBDHVBAVMVPDFGJWKKVCVDVEVH $.
      $( [10-Jun-2010] $)

    $( Negation in a ring is the same as right multiplication by ` -u 1 ` .
       (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    ringnegmn1r $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( A H ( N ` U ) ) ) $=
      ( wcel wa cfv co wceq crn mpdan adantr cring cgi c1st rneqi eqtri ring1cl
      ringnegcl ringodi 3exp2 imp43 ringaddneg2 opreq2d ringorz eqtrd ringoridm
      jca eqid 3eqtr3rd ringocl mpd3an3 cgr ringogrpo grpoinvid2 syl3an1 mpbird
      wb ) BUAMZAGMZNZAFOACFOZEPZQZVKADPZDUBOZQZVIAVJCDPZEPZVKACEPZDPZVNVMVIVJG
      MZCGMZNVQVSQZVIVTWAVGVTVHVGWAVTBCEGGDRBUCOZRJDWCHUDUEZILUFZCBDFGHJKUGSTZV
      GWAVHWETUPVGVHVTWAWBVGVHVTWAWBAVJCBDEGHIJUHUIUJSVIVQAVNEPVNVIVPVNAEVGVPVN
      QZVHVGWAWGWECBDFGVNHJKVNUQZUKSTULABDEGVNWHJHIUMUNVIVRAVKDABCEGIWDLUOULURV
      GVHVKGMZVLVOVFZVGVHVTWIWFAVJBDEGHIJUSUTVGDVAMVHWIWJBDHVBAVKVNDFGJWHKVCVDU
      TVE $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringnegmul.1 $e |- G = ( 1st ` R ) $.
    ringnegmul.2 $e |- H = ( 2nd ` R ) $.
    ringnegmul.3 $e |- X = ran G $.
    ringnegmul.4 $e |- N = ( inv ` G ) $.
    $( Negation of a product in a ring.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    ringneglmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( ( N ` A ) H B ) ) $=
      ( cring wcel w3a cfv co wceq wi crn ringnegmn1l cgi c1st rneqi eqtri eqid
      ring1cl ringnegcl mpdan ringoass 3exp2 3imp 3adant3 opreq1d ringocl 3expb
      mpd wa syldan 3impb 3eqtr4rd ) CLMZAGMZBGMZNZEUAOZFOZAEPZBEPZVFABEPZEPZAF
      OZBEPVIFOZVAVBVCVHVJQZVAVFGMZVBVCVMRRVAVEGMVNCVEEGGDSCUBOZSJDVOHUCUDIVEUE
      ZUFVECDFGHJKUGUHVAVNVBVCVMVFABCDEGHIJUIUJUPUKVDVKVGBEVAVBVKVGQVCACVEDEFGH
      IJKVPTULUMVAVBVCVLVJQZVAVBVCUQVIGMZVQVAVBVCVRABCDEGHIJUNUOVICVEDEFGHIJKVP
      TURUSUT $.
      $( [19-Jun-2010] $)

    $( Negation of a product in a ring.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    ringnegrmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( A H ( N ` B ) ) ) $=
      ( cring wcel w3a co cfv wceq wi crn ringnegmn1r cgi c1st rneqi eqtri eqid
      ring1cl ringnegcl mpdan ringoass 3exp2 com24 com34 mpd 3imp ringocl 3expb
      wa syldan 3impb 3adant2 opreq2d 3eqtr4d ) CLMZAGMZBGMZNZABEOZEUAPZFPZEOZA
      BVIEOZEOZVGFPZABFPZEOVCVDVEVJVLQZVCVIGMZVDVEVORRVCVHGMVPCVHEGGDSCUBPZSJDV
      QHUCUDIVHUEZUFVHCDFGHJKUGUHVCVPVEVDVOVCVDVEVPVOVCVDVEVPVOABVICDEGHIJUIUJU
      KULUMUNVCVDVEVMVJQZVCVDVEUQVGGMZVSVCVDVEVTABCDEGHIJUOUPVGCVHDEFGHIJKVRTUR
      USVFVNVKAEVCVEVNVKQVDBCVHDEFGHIJKVRTUTVAVB $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringsubdi.1 $e |- G = ( 1st ` R ) $.
    ringsubdi.2 $e |- H = ( 2nd ` R ) $.
    ringsubdi.3 $e |- X = ran G $.
    ringsubdi.4 $e |- D = ( /g ` G ) $.
    $( Ring multiplication distributes over subtraction.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    ringsubdi $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( A H ( B D C ) ) = ( ( A H B ) D ( A H C ) ) ) $=
      ( wcel w3a wa cfv co wceq idd syldan cring cgn ringnegcl ex 3anim123d imp
      ringodi ringnegrmul 3adant3r2 opreq2d ringsub 3adant3r1 ringocl 3adant3r3
      eqid eqtr4d jca 3expb 3eqtr4d ) EUAMZAHMZBHMZCHMZNZOZABCFUBPZPZFQZGQZABGQ
      ZACGQZVFPZFQZABCDQZGQVJVKDQZVEVIVJAVGGQZFQZVMUTVDVAVBVGHMZNZVIVQRUTVDVSUT
      VAVAVBVBVCVRUTVASUTVBSUTVCVRCEFVFHIKVFUOZUCUDUEUFABVGEFGHIJKUGTVEVLVPVJFU
      TVAVCVLVPRVBACEFGVFHIJKVTUHUIUJUPVEVNVHAGUTVBVCVNVHRVABCDEFVFHIKVTLUKULUJ
      UTVDVJHMZVKHMZOVOVMRZVEWAWBUTVAVBWAVCABEFGHIJKUMUNUTVAVCWBVBACEFGHIJKUMUI
      UQUTWAWBWCVJVKDEFVFHIKVTLUKURTUS $.
      $( [19-Jun-2010] $)

    $( Ring multiplication distributes over subtraction.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    ringsubdir $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( ( A D B ) H C ) = ( ( A H C ) D ( B H C ) ) ) $=
      ( wcel w3a wa cfv co wceq idd syldan cring cgn ringnegcl ex 3anim123d imp
      ringodir ringneglmul 3adant3r1 opreq2d eqtr4d ringsub 3adant3r3 3adant3r2
      eqid opreq1d ringocl jca 3expb 3eqtr4d ) EUAMZAHMZBHMZCHMZNZOZABFUBPZPZFQ
      ZCGQZACGQZBCGQZVGPZFQZABDQZCGQVKVLDQZVFVJVKVHCGQZFQZVNVAVEVBVHHMZVDNZVJVR
      RVAVEVTVAVBVBVCVSVDVDVAVBSVAVCVSBEFVGHIKVGUOZUCUDVAVDSUEUFAVHCEFGHIJKUGTV
      FVMVQVKFVAVCVDVMVQRVBBCEFGVGHIJKWAUHUIUJUKVFVOVICGVAVBVCVOVIRVDABDEFVGHIK
      WALULUMUPVAVEVKHMZVLHMZOVPVNRZVFWBWCVAVBVDWBVCACEFGHIJKUQUNVAVCVDWCVBBCEF
      GHIJKUQUIURVAWBWCWDVKVLDEFVGHIKWALULUSTUT $.
      $( [19-Jun-2010] $)
  $}

  ${
    zerdivempx.1 $e |- G = ( 1st ` R ) $.
    zerdivempx.2 $e |- H = ( 2nd ` R ) $.
    zerdivempx.3 $e |- Z = ( Id ` G ) $.
    zerdivempx.4 $e |- X = ran G $.
    zerdivempx.5 $e |- U = ( Id ` H ) $.
    $d A a $.  $d B a $.  $d H a $.  $d R a $.  $d X a $.  $d Z a $.
    $( In a unitary ring a left invertible element is not a zero divisor.
       Generalization of ~ zerdivemp1 by Frederic Line.  (Contributed by Jeff
       Madsen, 18-Apr-2010.) $)
    zerdivemp1x $p |- ( ( R e. RingOps /\ A e. X /\ E. a e. X ( a H A ) = U )
       -> ( B e. X -> ( ( A H B ) = Z -> B = Z ) ) ) $=
      ( wcel co wceq wi w3a wa cring cv wrex opreq2 simpl1 simpr1 simpr3 simpl3
      3jca jca ringoass eqtr ex opreq1 ringorz 3adant3 crn c1st cfv rneqi eqtri
      ringolidm 3adant2 simp1 simp2 simp3 a1d 3exp com14 com13 sylc com15 com24
      3eqtr3d syl eqcoms com25 com12 3imp syl6 3imp1 3syl pm2.43i 3exp1 syl5com
      rexlimiv ) CUAOZAGOZIUBZAFPZDQZIGUCZBGOZABFPZHQZBHQZRRZWLWHWGWQWKWHWGWQRZ
      RIGWIGOZWKWHWRWOWGWMWSWKWHSZWPWOWIWNFPZWIHFPZQZWGWMWTWPRRWNHWIFUDWGXCWMWT
      WPWGXCWMSZWTTZWPXEWGWSWHWMSZTWJBFPZXAQZXEWPRXEWGXFWGXCWMWTUEXEWSWHWMXDWSW
      KWHUFXDWSWKWHUGWGXCWMWTUHUIUJWIABCEFGJKMUKXEXHWPWGXCWMWTXHWPRXHXCWMWTWGWP
      XHXCXGXBQZWMWTWGWPRZRRXHXCXIXGXAXBULUMWTWMXIXJWSWKWHWMXIXJRRZWKWSWHXKRZWK
      XGDBFPZQZWSXLRWJDBFUNXNXIWHWMWSXJXIWHWMWSXJRRRZRXMXGXMXGQZXIXOXPXITXMXBQZ
      XOXMXGXBULXQWSWMWHXJWGWSWMWHXQWPWGWSWMWHXQWPRZRZWGWSWMSXBHQZXMBQZXSWGWSXT
      WMWICEFGHLMJKUOUPWGWMYAWSBCDFGKGEUQCURUSZUQMEYBJUTVANVBVCWHYAXTXRXQYAXTWH
      WPXQYAXTWHWPRXQYAXTSZWPWHYCXMXBBHXQYAXTVDXQYAXTVEXQYAXTVFVNVGVHVIVJVKVHVL
      VMVOUMVPVQVOVRVSVJVTVLWAVRWBWCWDWEVIVHWFVJVS $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d g h $.
    isdivrng1.1 $e |- G = ( 1st ` R ) $.
    isdivrng1.2 $e |- H = ( 2nd ` R ) $.
    isdivrng1.3 $e |- Z = ( Id ` G ) $.
    isdivrng1.4 $e |- X = ran G $.
    $( The predicate "is a division ring".  (Contributed by Jeff Madsen,
       8-Jun-2010.) $)
    isdivrng1 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\
             ( H |` ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. GrpOp ) ) $=
      ( vg vh cdrng wcel cfv cop cring csn cdif cgr wa c1st c2nd wceq cres wrel
      cxp crn cgi copab relopab df-drngo releqi mpbir 1st2nd mpan relrng adantr
      cv opeq12i eqeq2i cvv fvex eqeltri isdivrngo ax-mp sneqi difeq12i xpeq12i
      reseq2i eleq1i anbi2i bitr4i eleq1 anbi1d bibi12d mpbiri sylbir pm5.21nii
      wb ) ALMZAAUANZAUBNZOZUCZAPMZCDEQZRZWGUFZUDZSMZTZLUEZVTWDWLJURZKURZOPMWNW
      MUGWMUHNQRZWOUFUDSMTZJKUIZUEWPJKUJLWQJKUKULUMALUNUOWEWDWJPUEWEWDUPAPUNUOU
      QWDABCOZUCZVTWKVSZWRWCABWACWBFGUSUTWSWTWRLMZWRPMZWJTZVSXAXBCBUGZBUHNZQZRZ
      XGUFZUDZSMZTZXCCVAMXAXKVSCWBVAGAUBVBVCVABCVDVEWJXJXBWIXISWHXHCWGXGWGXGDXD
      WFXFIEXEHVFVGZXLVHVIVJVKVLWSVTXAWKXCAWRLVMWSWEXBWJAWRPVMVNVOVPVQVR $.
      $( [8-Jun-2010] $)

    $( The product of two nonzero elements of a division ring is nonzero.
       (Contributed by Jeff Madsen, 9-Jun-2010.) $)
    divrngcl $p |- ( ( R e. DivRingOps /\ A e. ( X \ { Z } )
                    /\ B e. ( X \ { Z } ) ) -> ( A H B ) e. ( X \ { Z } ) ) $=
      ( wcel cxp wa co wceq adantl cdm wss eleq2d cdrng cring csn cdif cres cgr
      isdivrng1 oprvres crn wi grpocl 3expib grporndm difss xpss12 mp2an ringsm
      eqid fdm syl sseq2d mpbiri ssdmres sylib adantr dmeqd dmxpid syl6eq eqtrd
      wf anbi12d 3imtr3d imp eqeltrrd 3impb syl3an1b ) CUALCUBLZEFGUCZUDZVSMZUE
      ZUFLZNZAVSLZBVSLZABEOZVSLZCDEFGHIJKUGWCWDWEWGWCWDWENZNABWAOZWFVSWHWIWFPWC
      ABVSVSEUHQWCWHWIVSLZWCAWAUIZLZBWKLZNZWIWKLZWHWJWBWNWOUJVQWBWLWMWOABWAWKWK
      URUKULQWCWLWDWMWEWCWKVSAWCWKWARZRZVSWBWKWQPVQWAUMQWCWQVTRVSWCWPVTVQWPVTPZ
      WBVQVTERZSZWRVQWTVTFFMZSZVSFSZXCXBFVRUNZXDVSFVSFUOUPVQWSXAVTVQXAFEVJWSXAP
      CDEFHIKUQXAFEUSUTVAVBVTEVCVDVEVFVSVGVHVIZTWCWKVSBXETVKWCWKVSWIXETVLVMVNVO
      VP $.
      $( [9-Jun-2010] $)

    $d H x y u v w z $.  $d X x y u v w z $.  $d Z x y u v w z $.
    $d R x y u v w z $.  $d U x y u v w z $.
    isdivrng2.5 $e |- U = ( Id ` H ) $.
    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible.  (Contributed by Jeff Madsen, 8-Jun-2010.) $)
    isdivrng2 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
             A. x e. ( X \ { Z } ) E. y e. ( X \ { Z } ) ( y H x ) = U ) ) ) $=
      ( wcel wa co wceq wrex adantr oprvres vu vv vw vz cdrng csn cdif cxp cres
      cring cgr wne cv wral isdivrng1 dvrunz sylbir crn cdm grporndm adantl wss
      difss xpss12 mp2an wf ringsm fdm sseq2d mpbiri ssdmres sylib dmeqd dmxpid
      syl a1i 3eqtrd eleq2d biimpar cgn cfv eqid grpoinvcl adantll cgi grpolinv
      cmagm cexid cmnd unmnd mndmgmid sseqtri rnplrnml mpbii c1st rneqi ring1cl
      cin eqtri eldifsn sylanbrc grpomnd mndismgm exidresid eqtrd opreq1 eqeq1d
      syl31anc rcla4ev syl11anc syldan wb ancoms rexbidva bitrd mpbid ralrimiva
      rexeqdv jca cvv fvex eqeltri rnex difexg ax-mp wfn fnssres sylancl eldifi
      ffn anim12i ringocl 3expb sylan2 adantlr wi sylan2b adantlrl w3a sylan
      opreq2 rexbidv rcla4v imdistanri zerdivemp1x syl3an3 syl3an2 necon3d impr
      ssrexv imp an32s ancom2s an42s eqeltrd ffnoprv 3anim123i ringoass 3adant3
      ralrimivva opreq1d simpr3 simpr1 foprrn 3adant3r1 3adant1 opreq2d 3eqtr4d
      3adantr3 anim1i sylibr adantrr ringolidm adantlrr rcla4va cbvrexv isgrpda
      impbida pm5.32i bitri ) CUENZCUJNZFGHUFZUGZUWDUHZUIZUKNZOZUWBDHULZBUMZAUM
      ZFPZDQZBUWDRZAUWDUNZOZOZCEFGHIJKLUOZUWBUWGUWPUWBUWGUWPUWHUWIUWOUWHUWAUWIU
      WRCDEFGHIJLKMUPUQZUWHUWNAUWDUWHUWKUWDNZOZUWJUWKUWFPZDQZBUWFURZRZUWNUWHUWT
      UWKUXDNZUXEUWHUXFUWTUWHUXDUWDUWKUWHUXDUWFUSZUSZUWEUSZUWDUWGUXDUXHQUWBUWFU
      TVAUWHUXGUWEUWHUWEFUSZVBZUXGUWEQUWBUXKUWGUWBUXKUWEGGUHZVBZUWDGVBZUXNUXMGU
      WCVCZUXOUWDGUWDGVDVEZUWBUXJUXLUWEUWBUXLGFVFZUXJUXLQCEFGIJLVGZUXLGFVHVOVIV
      JSUWEFVKVLVMUXIUWDQUWHUWDVNVPVQZVRVSUWHUXFOZUWKUWFVTWAZWAZUXDNZUYBUWKUWFP
      ZDQZUXEUWGUXFUYCUWBUWKUWFUYAUXDUXDWBZUYAWBZWCWDUXTUYDUWFWEWAZDUWGUXFUYDUY
      HQUWBUWKUYHUWFUYAUXDUYFUYHWBUYGWFWDUWHUYHDQZUXFUWHFWGWHWRNZUWDFURZVBZDUWD
      NZUWFWGNZUYIUWBUYJUWGUWBFWINUYJCFJWJFWKVOSUWBUYLUWGUWBUWDEURZVBUYLUWDGUYO
      UXOLWLUWBUYOUYKUWDCEFJIWMVIWNSUWHDGNZUWIUYMUWBUYPUWGCDFGGUYOCWOWAZURLEUYQ
      IWPWSZJMWQZSUWSDGHWTZXAUWGUYNUWBUWGUWFWINUYNUWFXBUWFXCVOVADFUWFUYKUWDUYKW
      BMUWFWBXDXHSXEUXCUYEBUYBUXDUWJUYBQUXBUYDDUWJUYBUWKUWFXFXGXIXJXKUXAUXEUXCB
      UWDRZUWNUXAUXCBUXDUWDUWHUXDUWDQUWTUXSSXRUWTVUAUWNXLUWHUWTUXCUWMBUWDUWTUWJ
      UWDNZOUXBUWLDVUBUWTUXBUWLQUWJUWKUWDUWDFTXMXGXNVAXOXPXQXSUWQUAUBUCDUDUWFUW
      DUWDXTNZUWQGXTNVUCGUYOXTLEEUYQXTICWOYAYBYCYBGUWCXTYDYEVPUWQUWFUWEYFZUAUMZ
      UBUMZUWFPZUWDNZUBUWDUNUAUWDUNUWEUWDUWFVFZUWQFUXLYFZUXMVUDUWBVUJUWPUWBUXQV
      UJUXRUXLGFYJVOSUXPUXLUWEFYGYHUWQVUHUAUBUWDUWDUWQVUEUWDNZVUFUWDNZOZOZVUGVU
      EVUFFPZUWDVUMVUGVUOQZUWQVUEVUFUWDUWDFTZVAVUNVUOGNZVUOHULZVUOUWDNZUWBVUMVU
      RUWPVUMUWBVUEGNZVUFGNZOVURVUKVVAVULVVBVUEGUWCYIZVUFGUWCYIZYKUWBVVAVVBVURV
      UEVUFCEFGIJLYLYMYNYOUWBUWOVUMVUSUWIUWBVULUWOVUKVUSUWOVUKOUWBVULOZUWJVUEFP
      ZDQZBUWDRZVUKOVUSVUKUWOVVHUWNVVHAVUEUWDUWKVUEQZUWMVVGBUWDVVIUWLVVFDUWKVUE
      UWJFUUAXGUUBZUUCUUDVVEVUKVVHVUSUWBVUKVVHOZVULVUSVULUWBVVKOZVVBVUFHULZOVUS
      VUFGHWTVVLVVBVVMVUSVVLVVBOVUOHVUFHVVLVVBVUOHQVUFHQYPZUWBVUKVVHVVBVVNYPZVU
      KUWBVVAVVHVVOVVCVVHUWBVVAVVGBGRZVVOUXNVVHVVPYPUXOVVGBUWDGUUJYEVUEVUFCDEFG
      HBIJKLMUUEUUFUUGYMUUKUUHUUIYQUULUUMYNUUNYRVUOGHWTXAZUUOUUTUAUBUWDUWDUWDUW
      FUUPXAZUWQVUKVULUCUMZUWDNZYSZOZVUOVVSFPZVUEVUFVVSFPZFPZVUGVVSUWFPZVUEVUFV
      VSUWFPZUWFPZUWBVWAVWCVWEQZUWPVWAUWBVVAVVBVVSGNZYSVWIVUKVVAVULVVBVVTVWJVVC
      VVDVVSGUWCYIUUQVUEVUFVVSCEFGIJLUURYNYOVWBVWFVUOVVSUWFPZVWCVWBVUGVUOVVSUWF
      VWAVUPUWQVUKVULVUPVVTVUQUUSVAUVAVWBVUTVVTVWKVWCQUWQVUKVULVUTVVTVVQUVIUWQV
      UKVULVVTUVBVUOVVSUWDUWDFTXJXEVWBVWHVUEVWGFPZVWEVWBVUKVWGUWDNZVWHVWLQUWQVU
      KVULVVTUVCUWQVUIVWAVWMVVRVUIVULVVTVWMVUKVUFVVSUWDUWDUWDUWFUVDUVEYTVUEVWGU
      WDUWDFTXJVWBVWGVWDVUEFVWAVWGVWDQZUWQVULVVTVWNVUKVUFVVSUWDUWDFTUVFVAUVGXEU
      VHUWBUWIUYMUWOUWBUWIOZUYPUWIOUYMUWBUYPUWIUYSUVJUYTUVKZUVLUWBUWIVUKDVUEUWF
      PZVUEQUWOVWOVUKOVWQDVUEFPZVUEVWOUYMVUKVWQVWRQVWPDVUEUWDUWDFTYTUWBVUKVWRVU
      EQZUWIVUKUWBVVAVWSVVCVUECDFGJUYRMUVMYNYOXEUVNUWBUWOVUKUDUMZVUEUWFPZDQZUDU
      WDRZUWIUWOVUKVXCUWBVUKUWOVXCVUKUWOVVHVXCUWNVVHAVUEUWDVVJUVOVVHVUKVWTVUEFP
      ZDQZUDUWDRZVXCVVGVXEBUDUWDUWJVWTQVVFVXDDUWJVWTVUEFXFXGUVPVUKVXCVXFVUKVXBV
      XEUDUWDVWTUWDNZVUKVXBVXEXLVXGVUKOVXAVXDDVWTVUEUWDUWDFTXGXMXNVSYQXKXMWDYRU
      VQUVRUVSUVT $.
      $( [8-Jun-2010] $)

    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    isdivrng3 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
                        A. x e. ( X \ { Z } ) E. y e. X ( y H x ) = U ) ) ) $=
      ( wcel wne cv co wceq wrex wa cdrng csn cdif wral isdivrng2 wb eldifi wss
      cring difss ssrexv ax-mp neeq1 biimparc ringolz opreq1 syl5ibrcom necon3d
      wi eqeq1d imp an4s anassrs pm3.2 syl5com eldifsn syl6ibr imdistanda ancom
      sylan2 3imtr4g reximdv2 impbid2 ralbidva pm5.32da pm5.32i bitri ) CUANCUI
      NZDHOZBPZAPZFQZDRZBGHUBZUCZSZAWEUDZTZTVRVSWCBGSZAWEUDZTZTABCDEFGHIJKLMUEV
      RWHWKVRVSWGWJVRVSTZWFWIAWEWAWENWLWAGNZWFWIUFWAGWDUGWLWMTZWFWIWEGUHWFWIUSG
      WDUJWCBWEGUKULWNWCWCBGWEWNWCVTGNZTWCVTWENZTWOWCTWPWCTWNWCWOWPWNWCTZWOWOVT
      HOZTZWPWQWRWOWSWLWMWCWRVRWMVSWCWRVSWCTVRWMTZWBHOZWRWCXAVSWBDHUMUNWTXAWRWT
      VTHWBHWTWBHRVTHRZHWAFQZHRWACEFGHKLIJUOXBWBXCHVTHWAFUPUTUQURVAVJVBVCWOWRVD
      VEVTGHVFVGVHWOWCVIWPWCVIVKVLVMVJVNVOVPVQ $.
      $( [10-Jun-2010] $)
  $}

