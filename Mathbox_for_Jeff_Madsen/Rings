$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ringnegcl.1 $e |- G = ( 1st ` R ) $.
    ringnegcl.2 $e |- X = ran G $.
    ringnegcl.3 $e |- N = ( inv ` G ) $.
    $( A ring is closed under negation.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    rngonegcl $p |- ( ( R e. RingOps /\ A e. X ) -> ( N ` A ) e. X ) $=
      ( crngo wcel cgr cfv rngogrpo grpoinvcl sylan ) BIJCKJAEJADLEJBCFMACDEGHN
      O $.
      $( [10-Jun-2010] $)

    ${
      ringaddneg.4 $e |- Z = ( Id ` G ) $.
      $( Adding the negative in a ring gives zero.  (Contributed by Jeff
         Madsen, 10-Jun-2010.) $)
      rngoaddneg1 $p |- ( ( R e. RingOps /\ A e. X )
            -> ( A G ( N ` A ) ) = Z ) $=
        ( crngo wcel cgr cfv co wceq rngogrpo grporinv sylan ) BKLCMLAELAADNCOF
        PBCGQAFCDEHJIRS $.
        $( [10-Jun-2010] $)

      $( Adding the negative in a ring gives zero.  (Contributed by Jeff
         Madsen, 10-Jun-2010.) $)
      rngoaddneg2 $p |- ( ( R e. RingOps /\ A e. X )
             -> ( ( N ` A ) G A ) = Z ) $=
        ( crngo wcel cgr cfv co wceq rngogrpo grpolinv sylan ) BKLCMLAELADNACOF
        PBCGQAFCDEHJIRS $.
        $( [10-Jun-2010] $)
    $}

    ringsub.4 $e |- D = ( /g ` G ) $.
    $( Subtraction in a ring, in terms of addition and negation.  (Contributed
       by Jeff Madsen, 19-Jun-2010.) $)
    rngosub $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                        -> ( A D B ) = ( A G ( N ` B ) ) ) $=
      ( crngo wcel cgr co cfv wceq rngogrpo grpodivval syl3an1 ) DLMENMAGMBGMAB
      COABFPEOQDEHRABCEFGIJKST $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringneg.1 $e |- G = ( 1st ` R ) $.
    ringneg.2 $e |- H = ( 2nd ` R ) $.
    ringneg.3 $e |- X = ran G $.
    ringneg.4 $e |- N = ( inv ` G ) $.
    ringneg.5 $e |- U = ( Id ` H ) $.
    $( Negation in a ring is the same as left multiplication by ` -u 1 ` .
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    rngonegmn1l $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( ( N ` U ) H A ) ) $=
      ( wcel wa cfv co wceq crn mpdan adantr crngo cgi c1st rneqi eqtri rngo1cl
      rngonegcl rngodir 3exp2 imp42 an32s eqid rngoaddneg1 opreq1d rngolz eqtrd
      jca rngolidm 3eqtr3rd wb rngocl 3expa rngogrpo grpoinvid1 syl3an1 mpd3an3
      cgr mpbird ) BUAMZAGMZNZAFOCFOZAEPZQZAVMDPZDUBOZQZVKCVLDPZAEPZCAEPZVMDPZV
      PVOVKCGMZVLGMZNZVSWAQZVIWDVJVIWBWCBCEGGDRBUCOZRJDWFHUDUEZILUFZVIWBWCWHCBD
      FGHJKUGSZUQTVIWDVJWEVIWBWCVJWEVIWBWCVJWECVLABDEGHIJUHUIUJUKSVKVSVPAEPVPVK
      VRVPAEVIVRVPQZVJVIWBWJWHCBDFGVPHJKVPULZUMSTUNABDEGVPWKJHIUOUPVKVTAVMDABCE
      GIWGLURUNUSVIVJVMGMZVNVQUTZVKWCWLVIWCVJWITVIWCVJWLVIWCVJWLVLABDEGHIJVAVBU
      KSVIDVGMVJWLWMBDHVCAVMVPDFGJWKKVDVEVFVH $.
      $( [10-Jun-2010] $)

    $( Negation in a ring is the same as right multiplication by ` -u 1 ` .
       (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    rngonegmn1r $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( A H ( N ` U ) ) ) $=
      ( wcel wa cfv co wceq crn mpdan adantr crngo cgi c1st rneqi eqtri rngo1cl
      rngonegcl jca rngodi 3exp2 imp43 eqid rngoaddneg2 opreq2d rngorz rngoridm
      eqtrd 3eqtr3rd wb rngocl mpd3an3 cgr rngogrpo grpoinvid2 syl3an1 mpbird )
      BUAMZAGMZNZAFOACFOZEPZQZVKADPZDUBOZQZVIAVJCDPZEPZVKACEPZDPZVNVMVIVJGMZCGM
      ZNVQVSQZVIVTWAVGVTVHVGWAVTBCEGGDRBUCOZRJDWCHUDUEZILUFZCBDFGHJKUGSTZVGWAVH
      WETUHVGVHVTWAWBVGVHVTWAWBAVJCBDEGHIJUIUJUKSVIVQAVNEPVNVIVPVNAEVGVPVNQZVHV
      GWAWGWECBDFGVNHJKVNULZUMSTUNABDEGVNWHJHIUOUQVIVRAVKDABCEGIWDLUPUNURVGVHVK
      GMZVLVOUSZVGVHVTWIWFAVJBDEGHIJUTVAVGDVBMVHWIWJBDHVCAVKVNDFGJWHKVDVEVAVF
      $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringnegmul.1 $e |- G = ( 1st ` R ) $.
    ringnegmul.2 $e |- H = ( 2nd ` R ) $.
    ringnegmul.3 $e |- X = ran G $.
    ringnegmul.4 $e |- N = ( inv ` G ) $.
    $( Negation of a product in a ring.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    rngoneglmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( ( N ` A ) H B ) ) $=
      ( crngo wcel w3a cfv co wceq wi crn rngonegmn1l cgi c1st rneqi eqtri eqid
      rngo1cl rngonegcl mpdan rngoass 3exp2 mpd 3adant3 opreq1d wa rngocl 3expb
      3imp syldan 3impb 3eqtr4rd ) CLMZAGMZBGMZNZEUAOZFOZAEPZBEPZVFABEPZEPZAFOZ
      BEPVIFOZVAVBVCVHVJQZVAVFGMZVBVCVMRRVAVEGMVNCVEEGGDSCUBOZSJDVOHUCUDIVEUEZU
      FVECDFGHJKUGUHVAVNVBVCVMVFABCDEGHIJUIUJUKUQVDVKVGBEVAVBVKVGQVCACVEDEFGHIJ
      KVPTULUMVAVBVCVLVJQZVAVBVCUNVIGMZVQVAVBVCVRABCDEGHIJUOUPVICVEDEFGHIJKVPTU
      RUSUT $.
      $( [19-Jun-2010] $)

    $( Negation of a product in a ring.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    rngonegrmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( A H ( N ` B ) ) ) $=
      ( crngo wcel w3a co cfv wceq wi crn rngonegmn1r cgi c1st rneqi eqtri eqid
      rngo1cl rngonegcl mpdan rngoass 3exp2 com24 com34 mpd rngocl 3expb syldan
      3imp wa 3impb 3adant2 opreq2d 3eqtr4d ) CLMZAGMZBGMZNZABEOZEUAPZFPZEOZABV
      IEOZEOZVGFPZABFPZEOVCVDVEVJVLQZVCVIGMZVDVEVORRVCVHGMVPCVHEGGDSCUBPZSJDVQH
      UCUDIVHUEZUFVHCDFGHJKUGUHVCVPVEVDVOVCVDVEVPVOVCVDVEVPVOABVICDEGHIJUIUJUKU
      LUMUQVCVDVEVMVJQZVCVDVEURVGGMZVSVCVDVEVTABCDEGHIJUNUOVGCVHDEFGHIJKVRTUPUS
      VFVNVKAEVCVEVNVKQVDBCVHDEFGHIJKVRTUTVAVB $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringsubdi.1 $e |- G = ( 1st ` R ) $.
    ringsubdi.2 $e |- H = ( 2nd ` R ) $.
    ringsubdi.3 $e |- X = ran G $.
    ringsubdi.4 $e |- D = ( /g ` G ) $.
    $( Ring multiplication distributes over subtraction.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    rngosubdi $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( A H ( B D C ) ) = ( ( A H B ) D ( A H C ) ) ) $=
      ( wcel w3a wa cfv co wceq idd syldan crngo cgn rngonegcl ex 3anim123d imp
      eqid rngodi rngonegrmul 3adant3r2 opreq2d eqtr4d rngosub 3adant3r1 rngocl
      3adant3r3 jca 3expb 3eqtr4d ) EUAMZAHMZBHMZCHMZNZOZABCFUBPZPZFQZGQZABGQZA
      CGQZVFPZFQZABCDQZGQVJVKDQZVEVIVJAVGGQZFQZVMUTVDVAVBVGHMZNZVIVQRUTVDVSUTVA
      VAVBVBVCVRUTVASUTVBSUTVCVRCEFVFHIKVFUGZUCUDUEUFABVGEFGHIJKUHTVEVLVPVJFUTV
      AVCVLVPRVBACEFGVFHIJKVTUIUJUKULVEVNVHAGUTVBVCVNVHRVABCDEFVFHIKVTLUMUNUKUT
      VDVJHMZVKHMZOVOVMRZVEWAWBUTVAVBWAVCABEFGHIJKUOUPUTVAVCWBVBACEFGHIJKUOUJUQ
      UTWAWBWCVJVKDEFVFHIKVTLUMURTUS $.
      $( [19-Jun-2010] $)

    $( Ring multiplication distributes over subtraction.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    rngosubdir $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( ( A D B ) H C ) = ( ( A H C ) D ( B H C ) ) ) $=
      ( wcel w3a wa cfv co wceq idd syldan crngo cgn rngonegcl ex 3anim123d imp
      rngodir rngoneglmul 3adant3r1 opreq2d rngosub 3adant3r3 opreq1d 3adant3r2
      eqid eqtr4d rngocl jca 3expb 3eqtr4d ) EUAMZAHMZBHMZCHMZNZOZABFUBPZPZFQZC
      GQZACGQZBCGQZVGPZFQZABDQZCGQVKVLDQZVFVJVKVHCGQZFQZVNVAVEVBVHHMZVDNZVJVRRV
      AVEVTVAVBVBVCVSVDVDVAVBSVAVCVSBEFVGHIKVGUOZUCUDVAVDSUEUFAVHCEFGHIJKUGTVFV
      MVQVKFVAVCVDVMVQRVBBCEFGVGHIJKWAUHUIUJUPVFVOVICGVAVBVCVOVIRVDABDEFVGHIKWA
      LUKULUMVAVEVKHMZVLHMZOVPVNRZVFWBWCVAVBVDWBVCACEFGHIJKUQUNVAVCVDWCVBBCEFGH
      IJKUQUIURVAWBWCWDVKVLDEFVGHIKWALUKUSTUT $.
      $( [19-Jun-2010] $)
  $}

  ${
    zerdivempx.1 $e |- G = ( 1st ` R ) $.
    zerdivempx.2 $e |- H = ( 2nd ` R ) $.
    zerdivempx.3 $e |- Z = ( Id ` G ) $.
    zerdivempx.4 $e |- X = ran G $.
    zerdivempx.5 $e |- U = ( Id ` H ) $.
    $d A a $.  $d B a $.  $d H a $.  $d R a $.  $d X a $.  $d Z a $.
    $( In a unitary ring a left invertible element is not a zero divisor.
       Generalization of ~ zerdivemp1 by Frederic Line.  (Contributed by Jeff
       Madsen, 18-Apr-2010.) $)
    zerdivemp1x $p |- ( ( R e. RingOps /\ A e. X /\ E. a e. X ( a H A ) = U )
       -> ( B e. X -> ( ( A H B ) = Z -> B = Z ) ) ) $=
      ( wcel co wceq wi w3a wa crngo cv wrex opreq2 simpl1 simpr1 simpr3 simpl3
      3jca jca rngoass eqtr opreq1 rngorz 3adant3 crn c1st rneqi eqtri rngolidm
      ex cfv 3adant2 simp1 simp2 simp3 3eqtr3d a1d 3exp com14 com13 com15 com24
      sylc syl eqcoms com25 com12 3imp syl6 3imp1 3syl pm2.43i syl5com rexlimiv
      3exp1 ) CUAOZAGOZIUBZAFPZDQZIGUCZBGOZABFPZHQZBHQZRRZWLWHWGWQWKWHWGWQRZRIG
      WIGOZWKWHWRWOWGWMWSWKWHSZWPWOWIWNFPZWIHFPZQZWGWMWTWPRRWNHWIFUDWGXCWMWTWPW
      GXCWMSZWTTZWPXEWGWSWHWMSZTWJBFPZXAQZXEWPRXEWGXFWGXCWMWTUEXEWSWHWMXDWSWKWH
      UFXDWSWKWHUGWGXCWMWTUHUIUJWIABCEFGJKMUKXEXHWPWGXCWMWTXHWPRXHXCWMWTWGWPXHX
      CXGXBQZWMWTWGWPRZRRXHXCXIXGXAXBULVAWTWMXIXJWSWKWHWMXIXJRRZWKWSWHXKRZWKXGD
      BFPZQZWSXLRWJDBFUMXNXIWHWMWSXJXIWHWMWSXJRRRZRXMXGXMXGQZXIXOXPXITXMXBQZXOX
      MXGXBULXQWSWMWHXJWGWSWMWHXQWPWGWSWMWHXQWPRZRZWGWSWMSXBHQZXMBQZXSWGWSXTWMW
      ICEFGHLMJKUNUOWGWMYAWSBCDFGKGEUPCUQVBZUPMEYBJURUSNUTVCWHYAXTXRXQYAXTWHWPX
      QYAXTWHWPRXQYAXTSZWPWHYCXMXBBHXQYAXTVDXQYAXTVEXQYAXTVFVGVHVIVJVKVNVIVLVMV
      OVAVPVQVOVRVSVKVTVLWAVRWBWCWFWDVJVIWEVKVS $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d g h $.
    isdivrng1.1 $e |- G = ( 1st ` R ) $.
    isdivrng1.2 $e |- H = ( 2nd ` R ) $.
    isdivrng1.3 $e |- Z = ( Id ` G ) $.
    isdivrng1.4 $e |- X = ran G $.
    $( The predicate "is a division ring".  (Contributed by Jeff Madsen,
       8-Jun-2010.) $)
    isdrngo1 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\
             ( H |` ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. GrpOp ) ) $=
      ( vg vh cdrng wcel cfv cop crngo csn cdif cgr wa c1st c2nd wceq cres wrel
      cxp cv crn copab relopab df-drngo releqi mpbir 1st2nd mpan relrngo adantr
      cgi wb opeq12i eqeq2i fvex eqeltri isdivrngo ax-mp sneqi difeq12i xpeq12i
      reseq2i eleq1i anbi2i bitr4i eleq1 anbi1d bibi12d mpbiri sylbir pm5.21nii
      cvv ) ALMZAAUANZAUBNZOZUCZAPMZCDEQZRZWGUFZUDZSMZTZLUEZVTWDWLJUGZKUGZOPMWN
      WMUHWMURNQRZWOUFUDSMTZJKUIZUEWPJKUJLWQJKUKULUMALUNUOWEWDWJPUEWEWDUPAPUNUO
      UQWDABCOZUCZVTWKUSZWRWCABWACWBFGUTVAWSWTWRLMZWRPMZWJTZUSXAXBCBUHZBURNZQZR
      ZXGUFZUDZSMZTZXCCVSMXAXKUSCWBVSGAUBVBVCVSBCVDVEWJXJXBWIXISWHXHCWGXGWGXGDX
      DWFXFIEXEHVFVGZXLVHVIVJVKVLWSVTXAWKXCAWRLVMWSWEXBWJAWRPVMVNVOVPVQVR $.
      $( [8-Jun-2010] $)

    $( The product of two nonzero elements of a division ring is nonzero.
       (Contributed by Jeff Madsen, 9-Jun-2010.) $)
    divrngcl $p |- ( ( R e. DivRingOps /\ A e. ( X \ { Z } )
                    /\ B e. ( X \ { Z } ) ) -> ( A H B ) e. ( X \ { Z } ) ) $=
      ( wcel cxp wa co wceq adantl cdm wss eleq2d cdrng crngo csn cdif cres cgr
      isdrngo1 oprvres crn wi eqid grpocl 3expib grporndm difss xpss12 mp2an wf
      rngosm fdm sseq2d mpbiri ssdmres sylib adantr dmeqd dmxpid syl6eq anbi12d
      syl eqtrd 3imtr3d imp eqeltrrd 3impb syl3an1b ) CUALCUBLZEFGUCZUDZVSMZUEZ
      UFLZNZAVSLZBVSLZABEOZVSLZCDEFGHIJKUGWCWDWEWGWCWDWENZNABWAOZWFVSWHWIWFPWCA
      BVSVSEUHQWCWHWIVSLZWCAWAUIZLZBWKLZNZWIWKLZWHWJWBWNWOUJVQWBWLWMWOABWAWKWKU
      KULUMQWCWLWDWMWEWCWKVSAWCWKWARZRZVSWBWKWQPVQWAUNQWCWQVTRVSWCWPVTVQWPVTPZW
      BVQVTERZSZWRVQWTVTFFMZSZVSFSZXCXBFVRUOZXDVSFVSFUPUQVQWSXAVTVQXAFEURWSXAPC
      DEFHIKUSXAFEUTVJVAVBVTEVCVDVEVFVSVGVHVKZTWCWKVSBXETVIWCWKVSWIXETVLVMVNVOV
      P $.
      $( [9-Jun-2010] $)

    $d H x y u v w z $.  $d X x y u v w z $.  $d Z x y u v w z $.
    $d R x y u v w z $.  $d U x y u v w z $.
    isdivrng2.5 $e |- U = ( Id ` H ) $.
    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible.  (Contributed by Jeff Madsen, 8-Jun-2010.) $)
    isdrngo2 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
             A. x e. ( X \ { Z } ) E. y e. ( X \ { Z } ) ( y H x ) = U ) ) ) $=
      ( wcel wa co wceq wrex adantr oprvres vu vv vw vz cdrng csn cdif cxp cres
      crngo cgr wne cv isdrngo1 dvrunz sylbir crn cdm grporndm adantl wss difss
      wral xpss12 mp2an rngosm fdm syl sseq2d mpbiri ssdmres sylib dmeqd dmxpid
      wf a1i 3eqtrd eleq2d biimpar cgn cfv grpoinvcl adantll cgi grpolinv cmagm
      eqid cexid cmnd rngomnd mndmgmid sseqtri rngorn1eq mpbii c1st rneqi eqtri
      rngo1cl eldifsn sylanbrc grpomnd mndismgm exidresid syl31anc eqtrd opreq1
      cin eqeq1d rcla4ev syl2anc syldan rexeqdv ancoms rexbidva bitrd ralrimiva
      wb mpbid jca cvv fvex eqeltri difexg ax-mp wfn ffn fnssres sylancl eldifi
      rnex anim12i rngocl 3expb sylan2 adantlr wi sylan2b adantlrl w3a sylan
      opreq2 rexbidv rcla4v imdistanri zerdivemp1x syl3an3 syl3an2 necon3d impr
      ssrexv imp an32s ancom2s an42s eqeltrd ralrimivva ffnoprv rngoass 3adant3
      3anim123i opreq1d 3adantr3 simpr3 simpr1 foprrn 3adant3r1 3adant1 opreq2d
      3eqtr4d adantrr rngolidm adantlrr rcla4va cbvrexv isgrpda impbida pm5.32i
      anim1i sylibr bitri ) CUENZCUJNZFGHUFZUGZUWDUHZUIZUKNZOZUWBDHULZBUMZAUMZF
      PZDQZBUWDRZAUWDVCZOZOZCEFGHIJKLUNZUWBUWGUWPUWBUWGUWPUWHUWIUWOUWHUWAUWIUWR
      CDEFGHIJLKMUOUPZUWHUWNAUWDUWHUWKUWDNZOZUWJUWKUWFPZDQZBUWFUQZRZUWNUWHUWTUW
      KUXDNZUXEUWHUXFUWTUWHUXDUWDUWKUWHUXDUWFURZURZUWEURZUWDUWGUXDUXHQUWBUWFUSU
      TUWHUXGUWEUWHUWEFURZVAZUXGUWEQUWBUXKUWGUWBUXKUWEGGUHZVAZUWDGVAZUXNUXMGUWC
      VBZUXOUWDGUWDGVDVEZUWBUXJUXLUWEUWBUXLGFVOZUXJUXLQCEFGIJLVFZUXLGFVGVHVIVJS
      UWEFVKVLVMUXIUWDQUWHUWDVNVPVQZVRVSUWHUXFOZUWKUWFVTWAZWAZUXDNZUYBUWKUWFPZD
      QZUXEUWGUXFUYCUWBUWKUWFUYAUXDUXDWGZUYAWGZWBWCUXTUYDUWFWDWAZDUWGUXFUYDUYHQ
      UWBUWKUYHUWFUYAUXDUYFUYHWGUYGWEWCUWHUYHDQZUXFUWHFWFWHXGNZUWDFUQZVAZDUWDNZ
      UWFWFNZUYIUWBUYJUWGUWBFWINUYJCFJWJFWKVHSUWBUYLUWGUWBUWDEUQZVAUYLUWDGUYOUX
      OLWLUWBUYOUYKUWDCEFJIWMVIWNSUWHDGNZUWIUYMUWBUYPUWGCDFGGUYOCWOWAZUQLEUYQIW
      PWQZJMWRZSUWSDGHWSZWTUWGUYNUWBUWGUWFWINUYNUWFXAUWFXBVHUTDFUWFUYKUWDUYKWGM
      UWFWGXCXDSXEUXCUYEBUYBUXDUWJUYBQUXBUYDDUWJUYBUWKUWFXFXHXIXJXKUXAUXEUXCBUW
      DRZUWNUXAUXCBUXDUWDUWHUXDUWDQUWTUXSSXLUWTVUAUWNXQUWHUWTUXCUWMBUWDUWTUWJUW
      DNZOUXBUWLDVUBUWTUXBUWLQUWJUWKUWDUWDFTXMXHXNUTXOXRXPXSUWQUAUBUCDUDUWFUWDU
      WDXTNZUWQGXTNVUCGUYOXTLEEUYQXTICWOYAYBYJYBGUWCXTYCYDVPUWQUWFUWEYEZUAUMZUB
      UMZUWFPZUWDNZUBUWDVCUAUWDVCUWEUWDUWFVOZUWQFUXLYEZUXMVUDUWBVUJUWPUWBUXQVUJ
      UXRUXLGFYFVHSUXPUXLUWEFYGYHUWQVUHUAUBUWDUWDUWQVUEUWDNZVUFUWDNZOZOZVUGVUEV
      UFFPZUWDVUMVUGVUOQZUWQVUEVUFUWDUWDFTZUTVUNVUOGNZVUOHULZVUOUWDNZUWBVUMVURU
      WPVUMUWBVUEGNZVUFGNZOVURVUKVVAVULVVBVUEGUWCYIZVUFGUWCYIZYKUWBVVAVVBVURVUE
      VUFCEFGIJLYLYMYNYOUWBUWOVUMVUSUWIUWBVULUWOVUKVUSUWOVUKOUWBVULOZUWJVUEFPZD
      QZBUWDRZVUKOVUSVUKUWOVVHUWNVVHAVUEUWDUWKVUEQZUWMVVGBUWDVVIUWLVVFDUWKVUEUW
      JFUUAXHUUBZUUCUUDVVEVUKVVHVUSUWBVUKVVHOZVULVUSVULUWBVVKOZVVBVUFHULZOVUSVU
      FGHWSVVLVVBVVMVUSVVLVVBOVUOHVUFHVVLVVBVUOHQVUFHQYPZUWBVUKVVHVVBVVNYPZVUKU
      WBVVAVVHVVOVVCVVHUWBVVAVVGBGRZVVOUXNVVHVVPYPUXOVVGBUWDGUUJYDVUEVUFCDEFGHB
      IJKLMUUEUUFUUGYMUUKUUHUUIYQUULUUMYNUUNYRVUOGHWSWTZUUOUUPUAUBUWDUWDUWDUWFU
      UQWTZUWQVUKVULUCUMZUWDNZYSZOZVUOVVSFPZVUEVUFVVSFPZFPZVUGVVSUWFPZVUEVUFVVS
      UWFPZUWFPZUWBVWAVWCVWEQZUWPVWAUWBVVAVVBVVSGNZYSVWIVUKVVAVULVVBVVTVWJVVCVV
      DVVSGUWCYIUUTVUEVUFVVSCEFGIJLUURYNYOVWBVWFVUOVVSUWFPZVWCVWBVUGVUOVVSUWFVW
      AVUPUWQVUKVULVUPVVTVUQUUSUTUVAVWBVUTVVTVWKVWCQUWQVUKVULVUTVVTVVQUVBUWQVUK
      VULVVTUVCVUOVVSUWDUWDFTXJXEVWBVWHVUEVWGFPZVWEVWBVUKVWGUWDNZVWHVWLQUWQVUKV
      ULVVTUVDUWQVUIVWAVWMVVRVUIVULVVTVWMVUKVUFVVSUWDUWDUWDUWFUVEUVFYTVUEVWGUWD
      UWDFTXJVWBVWGVWDVUEFVWAVWGVWDQZUWQVULVVTVWNVUKVUFVVSUWDUWDFTUVGUTUVHXEUVI
      UWBUWIUYMUWOUWBUWIOZUYPUWIOUYMUWBUYPUWIUYSUVRUYTUVSZUVJUWBUWIVUKDVUEUWFPZ
      VUEQUWOVWOVUKOVWQDVUEFPZVUEVWOUYMVUKVWQVWRQVWPDVUEUWDUWDFTYTUWBVUKVWRVUEQ
      ZUWIVUKUWBVVAVWSVVCVUECDFGJUYRMUVKYNYOXEUVLUWBUWOVUKUDUMZVUEUWFPZDQZUDUWD
      RZUWIUWOVUKVXCUWBVUKUWOVXCVUKUWOVVHVXCUWNVVHAVUEUWDVVJUVMVVHVUKVWTVUEFPZD
      QZUDUWDRZVXCVVGVXEBUDUWDUWJVWTQVVFVXDDUWJVWTVUEFXFXHUVNVUKVXCVXFVUKVXBVXE
      UDUWDVWTUWDNZVUKVXBVXEXQVXGVUKOVXAVXDDVWTVUEUWDUWDFTXHXMXNVSYQXKXMWCYRUVO
      UVPUVQUVT $.
      $( [8-Jun-2010] $)

    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    isdrngo3 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
                        A. x e. ( X \ { Z } ) E. y e. X ( y H x ) = U ) ) ) $=
      ( wcel wne cv co wceq wrex wa cdrng crngo csn cdif isdrngo2 wb eldifi wss
      wral wi difss ssrexv ax-mp neeq1 biimparc rngolz opreq1 eqeq1d syl5ibrcom
      necon3d imp sylan2 anassrs pm3.2 syl5com eldifsn syl6ibr imdistanda ancom
      an4s 3imtr4g reximdv2 impbid2 ralbidva pm5.32da pm5.32i bitri ) CUANCUBNZ
      DHOZBPZAPZFQZDRZBGHUCZUDZSZAWEUIZTZTVRVSWCBGSZAWEUIZTZTABCDEFGHIJKLMUEVRW
      HWKVRVSWGWJVRVSTZWFWIAWEWAWENWLWAGNZWFWIUFWAGWDUGWLWMTZWFWIWEGUHWFWIUJGWD
      UKWCBWEGULUMWNWCWCBGWEWNWCVTGNZTWCVTWENZTWOWCTWPWCTWNWCWOWPWNWCTZWOWOVTHO
      ZTZWPWQWRWOWSWLWMWCWRVRWMVSWCWRVSWCTVRWMTZWBHOZWRWCXAVSWBDHUNUOWTXAWRWTVT
      HWBHWTWBHRVTHRZHWAFQZHRWACEFGHKLIJUPXBWBXCHVTHWAFUQURUSUTVAVBVJVCWOWRVDVE
      VTGHVFVGVHWOWCVIWPWCVIVKVLVMVBVNVOVPVQ $.
      $( [10-Jun-2010] $)
  $}

