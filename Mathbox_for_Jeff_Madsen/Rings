$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d ph x y z $.  $d G x y z $.  $d H x y z $.  $d U x y $.
    isringd.1 $e |- G = ( 1st ` R ) $.
    isringd.2 $e |- H = ( 2nd ` R ) $.
    isringd.3 $e |- ( ph -> R e. ( _V X. _V ) ) $.
    isringd.4 $e |- ( ph -> G e. Abel ) $.
    isringd.5 $e |- ( ph -> X = ran G ) $.
    isringd.6 $e |- ( ph -> H : ( X X. X ) --> X ) $.
    isringd.7 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                                  -> ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $.
    isringd.8 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $.
    isringd.9 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) )
                          -> ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) ) $.
    isringd.10 $e |- ( ph -> U e. X ) $.
    isringd.11 $e |- ( ( ph /\ y e. X ) -> ( y H U ) = y ) $.
    isringd.12 $e |- ( ( ph /\ y e. X ) -> ( U H y ) = y ) $.
    $( Conditions that determine a ring. $)
    isringd $p |- ( ph -> R e. Ring ) $=
      ( cop cring c1st cfv c2nd cvv cxp wrel wcel wceq 1st2nd relxp sylancr
      opeq12i syl6eqr cabl crn wf wa cv co w3a wral wrex xpeq12 sylanc feq2d
      wb feq3 syl bitrd mpbid wi eleq2d 3anbi123d 3jca ex sylbird 3expd imp3a
      r19.21adv r19.21aivv opreq2 eqeq1d opreq1 anbi12d ralbidv rcla4ev
      eleqtrd jca r19.21aiv jca31 fvex eqeltri eqid isring ax-mp sylibr
      eqeltrd ) AEGHUBZUCAEEUDUEZEUFUEZUBZXAUGUGUHZUIEXEUJEXDUKAEXEULUGUGUMLUNG
      XBHXCJKUOUPAGUQUJZGURZXGUHZXGHUSZUTBVAZCVAZHVBZDVAZHVBXJXKXMHVBZHVBUKZXJX
      KXMGVBHVBXLXJXMHVBZGVBUKZXJXKGVBXMHVBXPXNGVBUKZVCZDXGVDZCXGVDBXGVDZXKXJHV
      BZXKUKZXLXKUKZUTZCXGVDZBXGVEZUTZUTZXAUCUJZAXFXIYHMAIIUHZIHUSZXIOAYLXHIHUS
      ZXIAYKXHIHIXGUKZYNYKXHUKAIXGIXGVFNNVGVHAYNYMXIVINIXGXHHVJVKVLVMAYAYGAXTBC
      XGXGAXJXGUJZXKXGUJZUTXSDXGAYOYPXMXGUJZXSVNAYOYPYQXSAYOYPYQVCXJIUJZXKIUJZX
      MIUJZVCZXSAYRYOYSYPYTYQAIXGXJNVOAIXGXKNVOZAIXGXMNVOVPAUUAXSAUUAUTXOXQXRPQ
      RVQVRVSVTWAWBWCFXGUJXKFHVBZXKUKZFXKHVBZXKUKZUTZCXGVDZYGAYFUUHBFXGXJFUKZYE
      UUGCXGUUIYCUUDYDUUFUUIYBUUCXKXJFXKHWDWEUUIXLUUEXKXJFXKHWFWEWGWHWIAFIXGSNW
      JAUUGCXGAYPYSUUGUUBAYSUUGAYSUTUUDUUFTUAWKVRVSWLVGWKWMHUGUJYJYIVIHXCUGKEUF
      WNWOBCDUGGHXGXGWPWQWRWSWT $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)
  $}

  ${
    ringnegcl.1 $e |- G = ( 1st ` R ) $.
    ringnegcl.2 $e |- X = ran G $.
    ringnegcl.3 $e |- N = ( inv ` G ) $.
    $( A ring is closed under negation. $)
    ringnegcl $p |- ( ( R e. Ring /\ A e. X ) -> ( N ` A ) e. X ) $=
      ( cgr wcel cfv cring grpinvcl ringgrp sylan ) CIJAEJADKEJBLJACDEGHMBCFNO
      $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)

    ${
      ringaddneg.4 $e |- Z = ( Id ` G ) $.
      $( Adding the negative in a ring gives zero. $)
      ringaddneg1 $p |- ( ( R e. Ring /\ A e. X ) -> ( A G ( N ` A ) ) = Z ) $=
        ( cgr wcel cfv co wceq cring grprinv ringgrp sylan ) CKLAELAADMCNFOBPLA
        FCDEHJIQBCGRS $.
        $( [25-Jun-2011] $) $( [10-Jun-2010] $)

      $( Adding the negative in a ring gives zero. $)
      ringaddneg2 $p |- ( ( R e. Ring /\ A e. X ) -> ( ( N ` A ) G A ) = Z ) $=
        ( cgr wcel cfv co wceq cring grplinv ringgrp sylan ) CKLAELADMACNFOBPLA
        FCDEHJIQBCGRS $.
        $( [25-Jun-2011] $) $( [10-Jun-2010] $)
    $}

    ringsub.4 $e |- D = ( /g ` G ) $.
    $( Subtraction in a ring, in terms of addition and negation. $)
    ringsub $p |- ( ( R e. Ring /\ A e. X /\ B e. X )
                                        -> ( A D B ) = ( A G ( N ` B ) ) ) $=
      ( cgr wcel co cfv wceq cring grpdivval ringgrp syl3an1 ) ELMAGMBGMABCNABF
      OENPDQMABCEFGIJKRDEHST $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)
  $}

  ${
    ringneg.1 $e |- G = ( 1st ` R ) $.
    ringneg.2 $e |- H = ( 2nd ` R ) $.
    ringneg.3 $e |- X = ran G $.
    ringneg.4 $e |- N = ( inv ` G ) $.
    ringneg.5 $e |- U = ( Id ` H ) $.
    $( Negation in a ring is the same as left multiplication by ` -u 1 ` . $)
    ringnegmn1l $p |- ( ( R e. Ring /\ A e. X ) ->
                                            ( N ` A ) = ( ( N ` U ) H A ) ) $=
      ( cring wcel wa cfv co wceq cgi crn c1st rneqi eqtri ring1cl ringnegcl
      mpdan jca adantr ringdir 3exp2 imp42 an1rs eqid ringaddneg1 opreq1d
      ringlz eqtrd ringlidm 3eqtr3rd wb ringcl 3expa cgr grpinvid1 ringgrp
      syl3an1 mpd3an3 mpbird ) BMNZAGNZOZAFPCFPZAEQZRZAVMDQZDSPZRZVKCVLDQZAEQZC
      AEQZVMDQZVPVOVKCGNZVLGNZOZVSWARZVIWDVJVIWBWCBCEGGDTBUAPZTJDWFHUBUCZILUDZV
      IWBWCWHCBDFGHJKUEUFZUGUHVIWDVJWEVIWBWCVJWEVIWBWCVJWECVLABDEGHIJUIUJUKULUF
      VKVSVPAEQVPVKVRVPAEVIVRVPRZVJVIWBWJWHCBDFGVPHJKVPUMZUNUFUHUOABDEGVPWKJHIU
      PUQVKVTAVMDABCEGIWGLURUOUSVIVJVMGNZVNVQUTZVKWCWLVIWCVJWIUHVIWCVJWLVIWCVJW
      LVLABDEGHIJVAVBULUFDVCNVJWLWMVIAVMVPDFGJWKKVDBDHVEVFVGVH $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)

    $( Negation in a ring is the same as right multiplication by ` -u 1 ` . $)
    ringnegmn1r $p |- ( ( R e. Ring /\ A e. X ) ->
                                            ( N ` A ) = ( A H ( N ` U ) ) ) $=
      ( cring wcel wa cfv co wceq cgi crn c1st rneqi eqtri ring1cl ringnegcl
      mpdan adantr jca ringdi 3exp2 imp43 eqid ringaddneg2 opreq2d ringrz
      eqtrd ringridm 3eqtr3rd wb ringcl mpd3an3 cgr grpinvid2 ringgrp syl3an1
      mpbird ) BMNZAGNZOZAFPACFPZEQZRZVKADQZDSPZRZVIAVJCDQZEQZVKACEQZDQZVNVMVIV
      JGNZCGNZOVQVSRZVIVTWAVGVTVHVGWAVTBCEGGDTBUAPZTJDWCHUBUCZILUDZCBDFGHJKUEUF
      UGZVGWAVHWEUGUHVGVHVTWAWBVGVHVTWAWBAVJCBDEGHIJUIUJUKUFVIVQAVNEQVNVIVPVNAE
      VGVPVNRZVHVGWAWGWECBDFGVNHJKVNULZUMUFUGUNABDEGVNWHJHIUOUPVIVRAVKDABCEGIWD
      LUQUNURVGVHVKGNZVLVOUSZVGVHVTWIWFAVJBDEGHIJUTVADVBNVHWIWJVGAVKVNDFGJWHKVC
      BDHVDVEVAVF $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)
  $}

  ${
    ringnegmul.1 $e |- G = ( 1st ` R ) $.
    ringnegmul.2 $e |- H = ( 2nd ` R ) $.
    ringnegmul.3 $e |- X = ran G $.
    ringnegmul.4 $e |- N = ( inv ` G ) $.
    $( Negation of a product in a ring. $)
    ringneglmul $p |- ( ( R e. Ring /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( ( N ` A ) H B ) ) $=
      ( cring wcel w3a cgi cfv co wceq wi crn c1st rneqi eqtri eqid ring1cl
      ringnegcl mpdan ringass 3exp2 mpd 3imp ringnegmn1l 3adant3 opreq1d wa
      ringcl 3expb syldan 3impb 3eqtr4rd ) CLMZAGMZBGMZNZEOPZFPZAEQZBEQZVFABEQZ
      EQZAFPZBEQVIFPZVAVBVCVHVJRZVAVFGMZVBVCVMSSVAVEGMVNCVEEGGDTCUAPZTJDVOHUBUC
      IVEUDZUEVECDFGHJKUFUGVAVNVBVCVMVFABCDEGHIJUHUIUJUKVDVKVGBEVAVBVKVGRVCACVE
      DEFGHIJKVPULUMUNVAVBVCVLVJRZVAVBVCUOVIGMZVQVAVBVCVRABCDEGHIJUPUQVICVEDEFG
      HIJKVPULURUSUT $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)

    $( Negation of a product in a ring. $)
    ringnegrmul $p |- ( ( R e. Ring /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( A H ( N ` B ) ) ) $=
      ( cring wcel w3a co cgi cfv wceq wi crn c1st rneqi eqtri eqid ring1cl
      ringnegcl mpdan ringass 3exp2 com24 com34 mpd 3imp wa ringcl 3expb
      ringnegmn1r syldan 3impb 3adant2 opreq2d 3eqtr4d ) CLMZAGMZBGMZNZABEOZEPQ
      ZFQZEOZABVIEOZEOZVGFQZABFQZEOVCVDVEVJVLRZVCVIGMZVDVEVOSSVCVHGMVPCVHEGGDTC
      UAQZTJDVQHUBUCIVHUDZUEVHCDFGHJKUFUGVCVPVEVDVOVCVDVEVPVOVCVDVEVPVOABVICDEG
      HIJUHUIUJUKULUMVCVDVEVMVJRZVCVDVEUNVGGMZVSVCVDVEVTABCDEGHIJUOUPVGCVHDEFGH
      IJKVRUQURUSVFVNVKAEVCVEVNVKRVDBCVHDEFGHIJKVRUQUTVAVB $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)
  $}

  ${
    ringsubdi.1 $e |- G = ( 1st ` R ) $.
    ringsubdi.2 $e |- H = ( 2nd ` R ) $.
    ringsubdi.3 $e |- X = ran G $.
    ringsubdi.4 $e |- D = ( /g ` G ) $.
    $( Ring multiplication distributes over subtraction. $)
    ringsubdi $p |- ( ( R e. Ring /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( A H ( B D C ) ) = ( ( A H B ) D ( A H C ) ) ) $=
      ( cring wcel w3a wa cgn cfv co wceq idd eqid ringnegcl ex 3anim123d imp
      ringdi syldan ringnegrmul 3adant3r2 opreq2d eqtr4d ringsub 3adant3r1
      ringcl 3adant3r3 jca 3expb 3eqtr4d ) EMNZAHNZBHNZCHNZOZPZABCFQRZRZFSZGSZA
      BGSZACGSZVFRZFSZABCDSZGSVJVKDSZVEVIVJAVGGSZFSZVMUTVDVAVBVGHNZOZVIVQTUTVDV
      SUTVAVAVBVBVCVRUTVAUAUTVBUAUTVCVRCEFVFHIKVFUBZUCUDUEUFABVGEFGHIJKUGUHVEVL
      VPVJFUTVAVCVLVPTVBACEFGVFHIJKVTUIUJUKULVEVNVHAGUTVBVCVNVHTVABCDEFVFHIKVTL
      UMUNUKUTVDVJHNZVKHNZPVOVMTZVEWAWBUTVAVBWAVCABEFGHIJKUOUPUTVAVCWBVBACEFGHI
      JKUOUJUQUTWAWBWCVJVKDEFVFHIKVTLUMURUHUS $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)

    $( Ring multiplication distributes over subtraction. $)
    ringsubdir $p |- ( ( R e. Ring /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( ( A D B ) H C ) = ( ( A H C ) D ( B H C ) ) ) $=
      ( cring wcel w3a wa cgn cfv co wceq idd eqid ringnegcl ex 3anim123d imp
      ringdir syldan ringneglmul 3adant3r1 opreq2d eqtr4d ringsub 3adant3r3
      opreq1d ringcl 3adant3r2 jca 3expb 3eqtr4d ) EMNZAHNZBHNZCHNZOZPZABFQRZRZ
      FSZCGSZACGSZBCGSZVGRZFSZABDSZCGSVKVLDSZVFVJVKVHCGSZFSZVNVAVEVBVHHNZVDOZVJ
      VRTVAVEVTVAVBVBVCVSVDVDVAVBUAVAVCVSBEFVGHIKVGUBZUCUDVAVDUAUEUFAVHCEFGHIJK
      UGUHVFVMVQVKFVAVCVDVMVQTVBBCEFGVGHIJKWAUIUJUKULVFVOVICGVAVBVCVOVITVDABDEF
      VGHIKWALUMUNUOVAVEVKHNZVLHNZPVPVNTZVFWBWCVAVBVDWBVCACEFGHIJKUPUQVAVCVDWCV
      BBCEFGHIJKUPUJURVAWBWCWDVKVLDEFVGHIKWALUMUSUHUT $.
      $( [25-Jun-2011] $) $( [19-Jun-2010] $)
  $}

  ${
    zerdivempx.1 $e |- G = ( 1st ` R ) $.
    zerdivempx.2 $e |- H = ( 2nd ` R ) $.
    zerdivempx.3 $e |- Z = ( Id ` G ) $.
    zerdivempx.4 $e |- X = ran G $.
    zerdivempx.5 $e |- U = ( Id ` H ) $.
    $d A a $.  $d B a $.  $d H a $.  $d R a $.  $d X a $.  $d Z a $.
    $( In a unitary ring a left invertible element is not a zero divisor.
       Generalization of ~ zerdivemp1 by Frederic Line. $)
    zerdivemp1x $p |- ( ( R e. Ring /\ A e. X /\ E. a e. X ( a H A ) = U ) ->
                                  ( B e. X -> ( ( A H B ) = Z -> B = Z ) ) ) $=
      ( cring wcel cv co wceq wrex wi w3a wa 3simpl1 3simpr1 3simpr3 3simpl3
      3jca jca ringass eqtr ex opreq1 3simp1 3simp2 3simp3 3eqtr3d a1d 3exp
      com14 com13 ringrz 3adant3 crn c1st cfv rneqi eqtri ringlidm 3adant2
      sylc com15 com24 syl eqcoms com25 com12 3imp syl6 3imp1 3syl pm2.43i
      3exp1 opreq2 syl5com r19.23aiv ) COPZAGPZIQZAFRZDSZIGTZBGPZABFRZHSZBHSZUA
      UAZWLWHWGWQWKWHWGWQUAZUAIGWIGPZWKWHWRWOWGWMWSWKWHUBZWPWGWIWNFRZWIHFRZSZWM
      WTWPUAUAWOWGXCWMWTWPWGXCWMUBZWTUCZWPXEWGWSWHWMUBZUCWJBFRZXASZXEWPUAXEWGXF
      WGXCWMWTUDXEWSWHWMXDWSWKWHUEXDWSWKWHUFWGXCWMWTUGUHUIWIABCEFGJKMUJXEXHWPWG
      XCWMWTXHWPUAXHXCWMWTWGWPXHXCXGXBSZWMWTWGWPUAZUAUAXHXCXIXGXAXBUKULWTWMXIXJ
      WSWKWHWMXIXJUAUAZWKWSWHXKUAZWKXGDBFRZSZWSXLUAWJDBFUMXNXIWHWMWSXJXIWHWMWSX
      JUAUAUAZUAXMXGXMXGSZXIXOXPXIUCXMXBSZXOXMXGXBUKXQWSWMWHXJWGWSWMWHXQWPWGWSW
      MWHXQWPUAZUAZXBHSZXMBSZXSWGWSWMUBWHYAXTXRXQYAXTWHWPXQYAXTWHWPUAXQYAXTUBZW
      PWHYBXMXBBHXQYAXTUNXQYAXTUOXQYAXTUPUQURUSUTVAWGWSXTWMWICEFGHLMJKVBVCWGWMY
      AWSBCDFGKGEVDCVEVFZVDMEYCJVGVHNVIVJVKUSVLVMVNULVOVPVNVQVRVAVSVLVTVQWAWBWC
      WNHWIFWDWEUTUSWFVAVR $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d g h $.
    isdivrng1.1 $e |- G = ( 1st ` R ) $.
    isdivrng1.2 $e |- H = ( 2nd ` R ) $.
    isdivrng1.3 $e |- Z = ( Id ` G ) $.
    isdivrng1.4 $e |- X = ran G $.
    $( The predicate "is a division ring". $)
    isdivrng1 $p |- ( R e. DivRing <-> ( R e. Ring /\
                      ( H |` ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. Grp ) ) $=
      ( vg vh cdrng wcel c1st cfv c2nd cop wceq cring csn cdif cxp cres cgr wa
      wrel cv crn cgi copab relopab df-drng releqi mpbir 1st2nd mpan relrng
      adantr wb opeq12i eqeq2i cvv fvex eqeltri isdivrng ax-mp sneqi difeq12i
      xpeq12i reseq2 eleq1i anbi2i bitr4i eleq1 anbi1d bibi12d mpbiri sylbir
      pm5.21nii ) ALMZAANOZAPOZQZRZASMZCDETZUAZWGUBZUCZUDMZUEZLUFZVTWDWLJUGZKUG
      ZQSMWNWMUHWMUIOTUAZWOUBUCUDMUEZJKUJZUFWPJKUKLWQJKULUMUNALUOUPWEWDWJSUFWEW
      DUQASUOUPURWDABCQZRZVTWKUSZWRWCABWACWBFGUTVAWSWTWRLMZWRSMZWJUEZUSXAXBCBUH
      ZBUIOZTZUAZXGUBZUCZUDMZUEZXCCVBMXAXKUSCWBVBGAPVCVDVBBCVEVFWJXJXBWIXIUDWHX
      HRWIXIRWGXGWGXGDXDWFXFIEXEHVGVHZXLVIWHXHCVJVFVKVLVMWSVTXAWKXCAWRLVNWSWEXB
      WJAWRSVNVOVPVQVRVS $.
      $( [25-Jun-2011] $) $( [8-Jun-2010] $)

    $( The product of two nonzero elements of a division ring is nonzero. $)
    divrngcl $p |- ( ( R e. DivRing /\ A e. ( X \ { Z } )
                    /\ B e. ( X \ { Z } ) ) -> ( A H B ) e. ( X \ { Z } ) ) $=
      ( cring wcel csn cdif cxp cres cgr wa co cdrng wceq oprvres adantl crn
      wi eqid grpcl 3expib cdm grprndm wss difss xpss12 mp2an wf ringsm fdm
      syl sseq2d mpbiri ssdmres sylib adantr dmeqd dmxpid syl6eq eqtrd eleq2d
      anbi12d 3imtr3d imp eqeltrrd 3impb isdivrng1 syl3an1b ) CLMZEFGNZOZVSPZQZ
      RMZSZAVSMZBVSMZABETZVSMZCUAMWCWDWEWGWCWDWESZSABWATZWFVSWHWIWFUBWCABVSVSEU
      CUDWCWHWIVSMZWCAWAUEZMZBWKMZSZWIWKMZWHWJWBWNWOUFVQWBWLWMWOABWAWKWKUGUHUIU
      DWCWLWDWMWEWCWKVSAWCWKWAUJZUJZVSWBWKWQUBVQWAUKUDWCWQVTUJVSWCWPVTVQWPVTUBZ
      WBVQVTEUJZULZWRVQWTVTFFPZULZVSFULZXCXBFVRUMZXDVSFVSFUNUOVQWSXAVTVQXAFEUPW
      SXAUBCDEFHIKUQXAFEURUSUTVAVTEVBVCVDVEVSVFVGVHZVIWCWKVSBXEVIVJWCWKVSWIXEVI
      VKVLVMVNCDEFGHIJKVOVP $.
      $( [25-Jun-2011] $) $( [9-Jun-2010] $)

    $d H x y u v w z $.  $d X x y u v w z $.  $d Z x y u v w z $.
    $d R x y u v w z $.  $d U x y u v w z $.
    isdivrng2.5 $e |- U = ( Id ` H ) $.
    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible. $)
    isdivrng2 $p |- ( R e. DivRing <-> ( R e. Ring /\ ( U =/= Z /\
             A. x e. ( X \ { Z } ) E. y e. ( X \ { Z } ) ( y H x ) = U ) ) ) $=
      ( vu vv vw vz cdrng wcel cring csn cdif cxp cres cgr wa wne cv co wceq
      wrex wral isdivrng1 dvrunz sylbir crn cdm grprndm adantl wss difss
      xpss12 mp2an wf ringsm fdm syl sseq2d mpbiri adantr ssdmres sylib dmeqd
      dmxpid a1i 3eqtrd eleq2d biimpar cgn cfv opreq1 eqeq1d rcla4ev eqid
      grpinvcl adantll cgi grplinv cmagm cexid cin w3a exidresid cmnd unmnd
      mndmgmid sseqtri rnplrnml mpbii eldifsn c1st rneqi eqtri ring1cl
      sylanbrc 3jca grpmnd mndismgm sylanc eqtrd syldan rexeqdv wb oprvres
      ancoms rexbidva bitrd mpbid r19.21aiva jca cvv fvex eqeltri rnex difexg
      ax-mp wfn ffnoprv fnssres ffn sylancl ringcl 3expb eldifi anim12i sylan2
      adantlr wi zerdivemp1x ssrexv syl3an3 syl3an2 imp necon3d impr sylan2b
      an1rs ancom2s opreq2 rexbidv rcla4v imdistanri an42s adantlrl eqeltrd
      r19.21aivva ringass 3anim123i 3adant3 opreq1d 3adantr3 3simpr3 3simpr1
      foprrn 3adant3r1 sylan 3adant1 opreq2d 3eqtr4d anim1i sylibr adantrr
      ringlidm adantlrr rcla4va cbvrexv isgrpda impbida pm5.32i bitri ) CRSZCTS
      ZFGHUAZUBZUWDUCZUDZUESZUFZUWBDHUGZBUHZAUHZFUIZDUJZBUWDUKZAUWDULZUFZUFZCEF
      GHIJKLUMZUWBUWGUWPUWBUWGUWPUWHUWIUWOUWHUWAUWIUWRCDEFGHIJLKMUNUOZUWHUWNAUW
      DUWHUWKUWDSZUFZUWJUWKUWFUIZDUJZBUWFUPZUKZUWNUWHUWTUWKUXDSZUXEUWHUXFUWTUWH
      UXDUWDUWKUWHUXDUWFUQZUQZUWEUQZUWDUWGUXDUXHUJUWBUWFURUSUWHUXGUWEUWHUWEFUQZ
      UTZUXGUWEUJUWBUXKUWGUWBUXKUWEGGUCZUTZUWDGUTZUXNUXMGUWCVAZUXOUWDGUWDGVBVCZ
      UWBUXJUXLUWEUWBUXLGFVDZUXJUXLUJCEFGIJLVEZUXLGFVFVGVHVIVJUWEFVKVLVMUXIUWDU
      JUWHUWDVNVOVPZVQVRUWKUWFVSVTZVTZUXDSZUYAUWKUWFUIZDUJZUXEUWHUXFUFZUXCUYDBU
      YAUXDUWJUYAUJUXBUYCDUWJUYAUWKUWFWAWBWCUWGUXFUYBUWBUWKUWFUXTUXDUXDWDZUXTWD
      ZWEWFUYEUYCUWFWGVTZDUWGUXFUYCUYHUJUWBUWKUYHUWFUXTUXDUYFUYHWDUYGWHWFUWHUYH
      DUJZUXFFWIWJWKSZUWDFUPZUTZDUWDSZWLUWFWISZUYIUWHDFUWFUYKUWDUYKWDMUWFWDWMUW
      HUYJUYLUYMUWBUYJUWGUWBFWNSUYJCFJWOFWPVGVJUWBUYLUWGUWBUWDEUPZUTUYLUWDGUYOU
      XOLWQUWBUYOUYKUWDCEFJIWRVHWSVJUYMDGSZUWIUWHDGHWTZUWBUYPUWGCDFGGUYOCXAVTZU
      PLEUYRIXBXCZJMXDZVJUWSXEXFUWGUYNUWBUWGUWFWNSUYNUWFXGUWFXHVGUSXIVJXJXIXKUX
      AUXEUXCBUWDUKZUWNUXAUXCBUXDUWDUWHUXDUWDUJUWTUXSVJXLUWTVUAUWNXMUWHUWTUXCUW
      MBUWDUWTUWJUWDSZUFUXBUWLDVUBUWTUXBUWLUJUWJUWKUWDUWDFXNXOWBXPUSXQXRXSXTUWQ
      NOPDQUWFUWDUWDYASZUWQGYASVUCGUYOYALEEUYRYAICXAYBYCYDYCGUWCYAYEYFVOUWEUWDU
      WFVDZUWFUWEYGZNUHZOUHZUWFUIZUWDSZOUWDULNUWDULUWQNOUWDUWDUWDUWFYHFUXLYGZUX
      MVUEUWQUXLUWEFYIUWBVUJUWPUWBUXQVUJUXRUXLGFYJVGVJUXPYKUWQVUINOUWDUWDUWQVUF
      UWDSZVUGUWDSZUFZUFZVUHVUFVUGFUIZUWDVUMVUHVUOUJZUWQVUFVUGUWDUWDFXNZUSVUOUW
      DSZVUOGSZVUOHUGZVUNVUOGHWTUWBVUMVUSUWPUWBVUFGSZVUGGSZUFVUSVUMUWBVVAVVBVUS
      VUFVUGCEFGIJLYLYMVUKVVAVULVVBVUFGUWCYNZVUGGUWCYNZYOYPYQUWBUWOVUMVUTUWIUWB
      VULUWOVUKVUTUWBVULUFZUWJVUFFUIZDUJZBUWDUKZVUKUFVUTUWOVUKUFVVEVUKVVHVUTUWB
      VUKVVHUFZVULVUTUWBVVIUFZVVBVUGHUGZUFVUTVULVVJVVBVVKVUTVVJVVBUFVUOHVUGHVVJ
      VVBVUOHUJVUGHUJYRZUWBVUKVVHVVBVVLYRZUWBVVAVVHVVMVUKUWBVVAVVGBGUKZVVMVVHVU
      FVUGCDEFGHBIJKLMYSUXNVVHVVNYRUXOVVGBUWDGYTYFUUAVVCUUBYMUUCUUDUUEVUGGHWTUU
      FUUGUUHVUKUWOVVHUWNVVHAVUFUWDUWKVUFUJZUWMVVGBUWDVVOUWLVVFDUWKVUFUWJFUUIWB
      UUJZUUKUULYPUUMUUNXEZUUOUUPXEZUWQVUKVULPUHZUWDSZWLZUFZVUOVVSFUIZVUFVUGVVS
      FUIZFUIZVUHVVSUWFUIZVUFVUGVVSUWFUIZUWFUIZUWBVWAVWCVWEUJZUWPUWBVVAVVBVVSGS
      ZWLVWIVWAVUFVUGVVSCEFGIJLUUQVUKVVAVULVVBVVTVWJVVCVVDVVSGUWCYNUURYPYQVWBVW
      FVUOVVSUWFUIZVWCVWBVUHVUOVVSUWFVWAVUPUWQVUKVULVUPVVTVUQUUSUSUUTVURVVTVWKV
      WCUJVWBVUOVVSUWDUWDFXNUWQVUKVULVURVVTVVQUVAUWQVUKVULVVTUVBXIXJVWBVWHVUFVW
      GFUIZVWEVUKVWGUWDSZVWHVWLUJVWBVUFVWGUWDUWDFXNUWQVUKVULVVTUVCVUDVWAVWMUWQV
      UDVULVVTVWMVUKVUGVVSUWDUWDUWDUWFUVDUVEVVRUVFXIVWBVWGVWDVUFFVWAVWGVWDUJZUW
      QVULVVTVWNVUKVUGVVSUWDUWDFXNUVGUSUVHXJUVIUWBUWIUYMUWOUWBUWIUFZUYPUWIUFUYM
      UWBUYPUWIUYTUVJUYQUVKZUVLUWBUWIVUKDVUFUWFUIZVUFUJUWOVWOVUKUFVWQDVUFFUIZVU
      FUYMVUKVWQVWRUJVWODVUFUWDUWDFXNVWPUVFUWBVUKVWRVUFUJZUWIUWBVVAVWSVUKVUFCDF
      GJUYSMUVMVVCYPYQXJUVNUWBUWOVUKQUHZVUFUWFUIZDUJZQUWDUKZUWIUWOVUKVXCUWBVUKU
      WOVXCVUKUWOVVHVXCUWNVVHAVUFUWDVVPUVOVUKVWTVUFFUIZDUJZQUWDUKZVXCVVHVUKVXCV
      XFVUKVXBVXEQUWDVWTUWDSZVUKVXBVXEXMVXGVUKUFVXAVXDDVWTVUFUWDUWDFXNWBXOXPVRV
      VGVXEBQUWDUWJVWTUJVVFVXDDUWJVWTVUFFWAWBUVPUUFXKXOWFUUNUVQUVRUVSUVT $.
      $( [25-Jun-2011] $) $( [8-Jun-2010] $)

    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible. $)
    isdivrng3 $p |- ( R e. DivRing <-> ( R e. Ring /\ ( U =/= Z /\
                        A. x e. ( X \ { Z } ) E. y e. X ( y H x ) = U ) ) ) $=
      ( cdrng wcel cring wne cv co wceq csn cdif wrex wral wa isdivrng2 wb wss
      wi difss ssrexv ax-mp pm3.2 opreq1 eqeq1d ringlz syl5cbir necon3d imp
      neeq1 biimparc sylan2 an4s anassrs syl5com eldifsn syl6ibr imdistanda
      ancom 3imtr4g reximdv2 impbid2 eldifi ralbidva pm5.32da pm5.32i bitri )
      CNOCPOZDHQZBRZARZFSZDTZBGHUAZUBZUCZAWEUDZUEZUEVRVSWCBGUCZAWEUDZUEZUEABCDE
      FGHIJKLMUFVRWHWKVRVSWGWJVRVSUEZWFWIAWEWLWAGOZWFWIUGWAWEOWLWMUEZWFWIWEGUHW
      FWIUIGWDUJWCBWEGUKULWNWCWCBGWEWNWCVTGOZUEWCVTWEOZUEWOWCUEWPWCUEWNWCWOWPWN
      WCUEZWOWOVTHQZUEZWPWOWRWSWQWOWRUMWLWMWCWRVRWMVSWCWRVRWMUEZWBHQZWRVSWCUEWT
      XAWRWTVTHWBHVTHTZWBHTHWAFSZHTWTXBWBXCHVTHWAFUNUOWACEFGHKLIJUPUQURUSWCXAVS
      WBDHUTVAVBVCVDVEVTGHVFVGVHWOWCVIWPWCVIVJVKVLWAGWDVMVBVNVOVPVQ $.
      $( [25-Jun-2011] $) $( [10-Jun-2010] $)
  $}

  $( A field is a division ring. $)
  flddivrng $p |- ( K e. Fld -> K e. DivRing ) $=
    ( cfld wcel cdrng ccm2 cin df-fld eleq2i inss1 sseli sylbi ) ABCADEFZCADCBL
    AGHLDADEIJK $.
    $( [25-Jun-2011] $) $( [10-Jun-2010] $)

