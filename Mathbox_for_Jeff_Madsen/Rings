$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ringnegcl.1 $e |- G = ( 1st ` R ) $.
    ringnegcl.2 $e |- X = ran G $.
    ringnegcl.3 $e |- N = ( inv ` G ) $.
    $( A ring is closed under negation.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    ringnegcl $p |- ( ( R e. RingOps /\ A e. X ) -> ( N ` A ) e. X ) $=
      ( cring wcel cgr cfv ringogrpo grpoinvcl sylan ) BIJCKJAEJADLEJBCFMACDEGH
      NO $.
      $( [10-Jun-2010] $)

    ${
      ringaddneg.4 $e |- Z = ( Id ` G ) $.
      $( Adding the negative in a ring gives zero.  (Contributed by Jeff
         Madsen, 10-Jun-2010.) $)
      ringaddneg1 $p |- ( ( R e. RingOps /\ A e. X )
            -> ( A G ( N ` A ) ) = Z ) $=
        ( cring wcel cgr cfv co wceq ringogrpo grporinv sylan ) BKLCMLAELAADNCO
        FPBCGQAFCDEHJIRS $.
        $( [10-Jun-2010] $)

      $( Adding the negative in a ring gives zero.  (Contributed by Jeff
         Madsen, 10-Jun-2010.) $)
      ringaddneg2 $p |- ( ( R e. RingOps /\ A e. X )
             -> ( ( N ` A ) G A ) = Z ) $=
        ( cring wcel cgr cfv co wceq ringogrpo grpolinv sylan ) BKLCMLAELADNACO
        FPBCGQAFCDEHJIRS $.
        $( [10-Jun-2010] $)
    $}

    ringsub.4 $e |- D = ( /g ` G ) $.
    $( Subtraction in a ring, in terms of addition and negation.  (Contributed
       by Jeff Madsen, 19-Jun-2010.) $)
    ringsub $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                        -> ( A D B ) = ( A G ( N ` B ) ) ) $=
      ( cring wcel cgr co cfv wceq ringogrpo grpdivval syl3an1 ) DLMENMAGMBGMAB
      COABFPEOQDEHRABCEFGIJKST $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringneg.1 $e |- G = ( 1st ` R ) $.
    ringneg.2 $e |- H = ( 2nd ` R ) $.
    ringneg.3 $e |- X = ran G $.
    ringneg.4 $e |- N = ( inv ` G ) $.
    ringneg.5 $e |- U = ( Id ` H ) $.
    $( Negation in a ring is the same as left multiplication by ` -u 1 ` .
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    ringnegmn1l $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( ( N ` U ) H A ) ) $=
      ( cring wcel wa cfv co wceq cgi crn c1st rneqi eqtri ring1cl ringnegcl
      mpdan jca adantr ringodir 3exp2 imp42 an32s eqid ringaddneg1 opreq1d
      ringolz eqtrd ringolidm 3eqtr3rd wb ringocl 3expa cgr ringogrpo
      grpoinvid1 syl3an1 mpd3an3 mpbird ) BMNZAGNZOZAFPCFPZAEQZRZAVMDQZDSPZRZVK
      CVLDQZAEQZCAEQZVMDQZVPVOVKCGNZVLGNZOZVSWARZVIWDVJVIWBWCBCEGGDTBUAPZTJDWFH
      UBUCZILUDZVIWBWCWHCBDFGHJKUEUFZUGUHVIWDVJWEVIWBWCVJWEVIWBWCVJWECVLABDEGHI
      JUIUJUKULUFVKVSVPAEQVPVKVRVPAEVIVRVPRZVJVIWBWJWHCBDFGVPHJKVPUMZUNUFUHUOAB
      DEGVPWKJHIUPUQVKVTAVMDABCEGIWGLURUOUSVIVJVMGNZVNVQUTZVKWCWLVIWCVJWIUHVIWC
      VJWLVIWCVJWLVLABDEGHIJVAVBULUFVIDVCNVJWLWMBDHVDAVMVPDFGJWKKVEVFVGVH $.
      $( [10-Jun-2010] $)

    $( Negation in a ring is the same as right multiplication by ` -u 1 ` .
       (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    ringnegmn1r $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( A H ( N ` U ) ) ) $=
      ( cring wcel wa cfv co wceq cgi crn c1st rneqi eqtri ring1cl ringnegcl
      mpdan adantr jca ringodi 3exp2 imp43 eqid ringaddneg2 opreq2d ringorz
      eqtrd ringoridm 3eqtr3rd wb ringocl mpd3an3 cgr ringogrpo grpoinvid2
      syl3an1 mpbird ) BMNZAGNZOZAFPACFPZEQZRZVKADQZDSPZRZVIAVJCDQZEQZVKACEQZDQ
      ZVNVMVIVJGNZCGNZOVQVSRZVIVTWAVGVTVHVGWAVTBCEGGDTBUAPZTJDWCHUBUCZILUDZCBDF
      GHJKUEUFUGZVGWAVHWEUGUHVGVHVTWAWBVGVHVTWAWBAVJCBDEGHIJUIUJUKUFVIVQAVNEQVN
      VIVPVNAEVGVPVNRZVHVGWAWGWECBDFGVNHJKVNULZUMUFUGUNABDEGVNWHJHIUOUPVIVRAVKD
      ABCEGIWDLUQUNURVGVHVKGNZVLVOUSZVGVHVTWIWFAVJBDEGHIJUTVAVGDVBNVHWIWJBDHVCA
      VKVNDFGJWHKVDVEVAVF $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringnegmul.1 $e |- G = ( 1st ` R ) $.
    ringnegmul.2 $e |- H = ( 2nd ` R ) $.
    ringnegmul.3 $e |- X = ran G $.
    ringnegmul.4 $e |- N = ( inv ` G ) $.
    $( Negation of a product in a ring.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    ringneglmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( ( N ` A ) H B ) ) $=
      ( cring wcel w3a cgi cfv co wceq wi crn c1st rneqi eqtri eqid ring1cl
      ringnegcl mpdan ringoass 3exp2 mpd 3imp ringnegmn1l 3adant3 opreq1d wa
      ringocl 3expb syldan 3impb 3eqtr4rd ) CLMZAGMZBGMZNZEOPZFPZAEQZBEQZVFABEQ
      ZEQZAFPZBEQVIFPZVAVBVCVHVJRZVAVFGMZVBVCVMSSVAVEGMVNCVEEGGDTCUAPZTJDVOHUBU
      CIVEUDZUEVECDFGHJKUFUGVAVNVBVCVMVFABCDEGHIJUHUIUJUKVDVKVGBEVAVBVKVGRVCACV
      EDEFGHIJKVPULUMUNVAVBVCVLVJRZVAVBVCUOVIGMZVQVAVBVCVRABCDEGHIJUPUQVICVEDEF
      GHIJKVPULURUSUT $.
      $( [19-Jun-2010] $)

    $( Negation of a product in a ring.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    ringnegrmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( A H ( N ` B ) ) ) $=
      ( cring wcel w3a co cgi cfv wceq wi crn c1st rneqi eqtri eqid ring1cl
      ringnegcl mpdan ringoass 3exp2 com24 com34 mpd 3imp wa ringocl 3expb
      ringnegmn1r syldan 3impb 3adant2 opreq2d 3eqtr4d ) CLMZAGMZBGMZNZABEOZEPQ
      ZFQZEOZABVIEOZEOZVGFQZABFQZEOVCVDVEVJVLRZVCVIGMZVDVEVOSSVCVHGMVPCVHEGGDTC
      UAQZTJDVQHUBUCIVHUDZUEVHCDFGHJKUFUGVCVPVEVDVOVCVDVEVPVOVCVDVEVPVOABVICDEG
      HIJUHUIUJUKULUMVCVDVEVMVJRZVCVDVEUNVGGMZVSVCVDVEVTABCDEGHIJUOUPVGCVHDEFGH
      IJKVRUQURUSVFVNVKAEVCVEVNVKRVDBCVHDEFGHIJKVRUQUTVAVB $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringsubdi.1 $e |- G = ( 1st ` R ) $.
    ringsubdi.2 $e |- H = ( 2nd ` R ) $.
    ringsubdi.3 $e |- X = ran G $.
    ringsubdi.4 $e |- D = ( /g ` G ) $.
    $( Ring multiplication distributes over subtraction.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    ringsubdi $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( A H ( B D C ) ) = ( ( A H B ) D ( A H C ) ) ) $=
      ( cring wcel w3a wa cgn cfv co wceq idd eqid ringnegcl ex 3anim123d imp
      ringodi syldan ringnegrmul 3adant3r2 opreq2d eqtr4d ringsub 3adant3r1
      ringocl 3adant3r3 jca 3expb 3eqtr4d ) EMNZAHNZBHNZCHNZOZPZABCFQRZRZFSZGSZ
      ABGSZACGSZVFRZFSZABCDSZGSVJVKDSZVEVIVJAVGGSZFSZVMUTVDVAVBVGHNZOZVIVQTUTVD
      VSUTVAVAVBVBVCVRUTVAUAUTVBUAUTVCVRCEFVFHIKVFUBZUCUDUEUFABVGEFGHIJKUGUHVEV
      LVPVJFUTVAVCVLVPTVBACEFGVFHIJKVTUIUJUKULVEVNVHAGUTVBVCVNVHTVABCDEFVFHIKVT
      LUMUNUKUTVDVJHNZVKHNZPVOVMTZVEWAWBUTVAVBWAVCABEFGHIJKUOUPUTVAVCWBVBACEFGH
      IJKUOUJUQUTWAWBWCVJVKDEFVFHIKVTLUMURUHUS $.
      $( [19-Jun-2010] $)

    $( Ring multiplication distributes over subtraction.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    ringsubdir $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( ( A D B ) H C ) = ( ( A H C ) D ( B H C ) ) ) $=
      ( cring wcel w3a wa cgn cfv co wceq idd eqid ringnegcl ex 3anim123d imp
      ringodir syldan ringneglmul 3adant3r1 opreq2d eqtr4d ringsub 3adant3r3
      opreq1d ringocl 3adant3r2 jca 3expb 3eqtr4d ) EMNZAHNZBHNZCHNZOZPZABFQRZR
      ZFSZCGSZACGSZBCGSZVGRZFSZABDSZCGSVKVLDSZVFVJVKVHCGSZFSZVNVAVEVBVHHNZVDOZV
      JVRTVAVEVTVAVBVBVCVSVDVDVAVBUAVAVCVSBEFVGHIKVGUBZUCUDVAVDUAUEUFAVHCEFGHIJ
      KUGUHVFVMVQVKFVAVCVDVMVQTVBBCEFGVGHIJKWAUIUJUKULVFVOVICGVAVBVCVOVITVDABDE
      FVGHIKWALUMUNUOVAVEVKHNZVLHNZPVPVNTZVFWBWCVAVBVDWBVCACEFGHIJKUPUQVAVCVDWC
      VBBCEFGHIJKUPUJURVAWBWCWDVKVLDEFVGHIKWALUMUSUHUT $.
      $( [19-Jun-2010] $)
  $}

  ${
    zerdivempx.1 $e |- G = ( 1st ` R ) $.
    zerdivempx.2 $e |- H = ( 2nd ` R ) $.
    zerdivempx.3 $e |- Z = ( Id ` G ) $.
    zerdivempx.4 $e |- X = ran G $.
    zerdivempx.5 $e |- U = ( Id ` H ) $.
    $d A a $.  $d B a $.  $d H a $.  $d R a $.  $d X a $.  $d Z a $.
    $( In a unitary ring a left invertible element is not a zero divisor.
       Generalization of ~ zerdivemp1 by Frederic Line.  (Contributed by Jeff
       Madsen, 18-Apr-2010.) $)
    zerdivemp1x $p |- ( ( R e. RingOps /\ A e. X /\ E. a e. X ( a H A ) = U )
       -> ( B e. X -> ( ( A H B ) = Z -> B = Z ) ) ) $=
      ( cring wcel cv co wceq wrex wi w3a opreq2 wa simpl1 simpr1 simpr3
      simpl3 3jca jca ringoass eqtr ex opreq1 ringorz 3adant3 crn c1st cfv
      rneqi eqtri ringolidm 3adant2 simp1 simp2 simp3 3eqtr3d a1d 3exp com14
      com13 sylc com15 com24 syl eqcoms com25 com12 3imp syl6 3imp1 3syl
      pm2.43i 3exp1 syl5com r19.23aiv ) COPZAGPZIQZAFRZDSZIGTZBGPZABFRZHSZBHSZU
      AUAZWLWHWGWQWKWHWGWQUAZUAIGWIGPZWKWHWRWOWGWMWSWKWHUBZWPWOWIWNFRZWIHFRZSZW
      GWMWTWPUAUAWNHWIFUCWGXCWMWTWPWGXCWMUBZWTUDZWPXEWGWSWHWMUBZUDWJBFRZXASZXEW
      PUAXEWGXFWGXCWMWTUEXEWSWHWMXDWSWKWHUFXDWSWKWHUGWGXCWMWTUHUIUJWIABCEFGJKMU
      KXEXHWPWGXCWMWTXHWPUAXHXCWMWTWGWPXHXCXGXBSZWMWTWGWPUAZUAUAXHXCXIXGXAXBULU
      MWTWMXIXJWSWKWHWMXIXJUAUAZWKWSWHXKUAZWKXGDBFRZSZWSXLUAWJDBFUNXNXIWHWMWSXJ
      XIWHWMWSXJUAUAUAZUAXMXGXMXGSZXIXOXPXIUDXMXBSZXOXMXGXBULXQWSWMWHXJWGWSWMWH
      XQWPWGWSWMWHXQWPUAZUAZWGWSWMUBXBHSZXMBSZXSWGWSXTWMWICEFGHLMJKUOUPWGWMYAWS
      BCDFGKGEUQCURUSZUQMEYBJUTVANVBVCWHYAXTXRXQYAXTWHWPXQYAXTWHWPUAXQYAXTUBZWP
      WHYCXMXBBHXQYAXTVDXQYAXTVEXQYAXTVFVGVHVIVJVKVLVIVMVNVOUMVPVQVOVRVSVKVTVMW
      AVRWBWCWDWEVJVIWFVKVS $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d g h $.
    isdivrng1.1 $e |- G = ( 1st ` R ) $.
    isdivrng1.2 $e |- H = ( 2nd ` R ) $.
    isdivrng1.3 $e |- Z = ( Id ` G ) $.
    isdivrng1.4 $e |- X = ran G $.
    $( The predicate "is a division ring".  (Contributed by Jeff Madsen,
       8-Jun-2010.) $)
    isdivrng1 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\
             ( H |` ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. GrpOp ) ) $=
      ( vg vh cdrng wcel c1st cfv c2nd cop wceq cring csn cdif cxp cres cgr wa
      wrel cv crn cgi copab relopab df-drngo releqi mpbir 1st2nd mpan relrng
      adantr wb opeq12i eqeq2i cvv fvex eqeltri isdivrngo ax-mp sneqi difeq12i
      xpeq12i reseq2i eleq1i anbi2i bitr4i eleq1 anbi1d bibi12d mpbiri sylbir
      pm5.21nii ) ALMZAANOZAPOZQZRZASMZCDETZUAZWGUBZUCZUDMZUEZLUFZVTWDWLJUGZKUG
      ZQSMWNWMUHWMUIOTUAZWOUBUCUDMUEZJKUJZUFWPJKUKLWQJKULUMUNALUOUPWEWDWJSUFWEW
      DUQASUOUPURWDABCQZRZVTWKUSZWRWCABWACWBFGUTVAWSWTWRLMZWRSMZWJUEZUSXAXBCBUH
      ZBUIOZTZUAZXGUBZUCZUDMZUEZXCCVBMXAXKUSCWBVBGAPVCVDVBBCVEVFWJXJXBWIXIUDWHX
      HCWGXGWGXGDXDWFXFIEXEHVGVHZXLVIVJVKVLVMWSVTXAWKXCAWRLVNWSWEXBWJAWRSVNVOVP
      VQVRVS $.
      $( [8-Jun-2010] $)

    $( The product of two nonzero elements of a division ring is nonzero.
       (Contributed by Jeff Madsen, 9-Jun-2010.) $)
    divrngcl $p |- ( ( R e. DivRingOps /\ A e. ( X \ { Z } )
                    /\ B e. ( X \ { Z } ) ) -> ( A H B ) e. ( X \ { Z } ) ) $=
      ( cdrng wcel cring csn cdif cxp cres cgr wa co isdivrng1 wceq oprvres
      adantl crn wi eqid grpocl 3expib cdm grprndm wss difss xpss12 mp2an wf
      ringsm fdm syl sseq2d mpbiri ssdmres sylib adantr dmeqd dmxpid syl6eq
      eqtrd eleq2d anbi12d 3imtr3d imp eqeltrrd 3impb syl3an1b ) CLMCNMZEFGOZPZ
      VSQZRZSMZTZAVSMZBVSMZABEUAZVSMZCDEFGHIJKUBWCWDWEWGWCWDWETZTABWAUAZWFVSWHW
      IWFUCWCABVSVSEUDUEWCWHWIVSMZWCAWAUFZMZBWKMZTZWIWKMZWHWJWBWNWOUGVQWBWLWMWO
      ABWAWKWKUHUIUJUEWCWLWDWMWEWCWKVSAWCWKWAUKZUKZVSWBWKWQUCVQWAULUEWCWQVTUKVS
      WCWPVTVQWPVTUCZWBVQVTEUKZUMZWRVQWTVTFFQZUMZVSFUMZXCXBFVRUNZXDVSFVSFUOUPVQ
      WSXAVTVQXAFEUQWSXAUCCDEFHIKURXAFEUSUTVAVBVTEVCVDVEVFVSVGVHVIZVJWCWKVSBXEV
      JVKWCWKVSWIXEVJVLVMVNVOVP $.
      $( [9-Jun-2010] $)

    $d H x y u v w z $.  $d X x y u v w z $.  $d Z x y u v w z $.
    $d R x y u v w z $.  $d U x y u v w z $.
    isdivrng2.5 $e |- U = ( Id ` H ) $.
    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible.  (Contributed by Jeff Madsen, 8-Jun-2010.) $)
    isdivrng2 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
             A. x e. ( X \ { Z } ) E. y e. ( X \ { Z } ) ( y H x ) = U ) ) ) $=
      ( vu vv vw vz cdrng wcel cring csn cdif cxp cres cgr wa wne cv co wceq
      wrex wral isdivrng1 dvrunz sylbir crn cdm grprndm adantl wss difss
      xpss12 mp2an wf ringsm fdm syl sseq2d mpbiri adantr ssdmres sylib dmeqd
      dmxpid a1i 3eqtrd eleq2d biimpar cgn cfv eqid grpoinvcl adantll cgi
      grpolinv cmagm cexid cin cmnd unmnd mndmgmid sseqtri rnplrnml mpbii c1st
      rneqi eqtri ring1cl eldifsn sylanbrc grpmnd mndismgm exidresid syl31anc
      eqtrd opreq1 eqeq1d rcla4ev syl11anc syldan rexeqdv wb oprvres ancoms
      rexbidva bitrd mpbid r19.21aiva jca cvv fvex eqeltri rnex difexg ax-mp
      wfn ffn fnssres sylancl eldifi anim12i ringocl 3expb sylan2 adantlr
      opreq2 rexbidv rcla4v imdistanri wi ssrexv zerdivemp1x syl3an3 syl3an2
      imp necon3d impr sylan2b an32s ancom2s an42s adantlrl eqeltrd
      r19.21aivva ffnoprv w3a 3anim123i ringoass 3adant3 opreq1d 3adantr3
      simpr3 simpr1 foprrn 3adant3r1 sylan 3adant1 opreq2d 3eqtr4d anim1i
      sylibr adantrr ringolidm adantlrr rcla4va cbvrexv isgrpda impbida
      pm5.32i bitri ) CRSZCTSZFGHUAZUBZUWDUCZUDZUESZUFZUWBDHUGZBUHZAUHZFUIZDUJZ
      BUWDUKZAUWDULZUFZUFZCEFGHIJKLUMZUWBUWGUWPUWBUWGUWPUWHUWIUWOUWHUWAUWIUWRCD
      EFGHIJLKMUNUOZUWHUWNAUWDUWHUWKUWDSZUFZUWJUWKUWFUIZDUJZBUWFUPZUKZUWNUWHUWT
      UWKUXDSZUXEUWHUXFUWTUWHUXDUWDUWKUWHUXDUWFUQZUQZUWEUQZUWDUWGUXDUXHUJUWBUWF
      URUSUWHUXGUWEUWHUWEFUQZUTZUXGUWEUJUWBUXKUWGUWBUXKUWEGGUCZUTZUWDGUTZUXNUXM
      GUWCVAZUXOUWDGUWDGVBVCZUWBUXJUXLUWEUWBUXLGFVDZUXJUXLUJCEFGIJLVEZUXLGFVFVG
      VHVIVJUWEFVKVLVMUXIUWDUJUWHUWDVNVOVPZVQVRUWHUXFUFZUWKUWFVSVTZVTZUXDSZUYBU
      WKUWFUIZDUJZUXEUWGUXFUYCUWBUWKUWFUYAUXDUXDWAZUYAWAZWBWCUXTUYDUWFWDVTZDUWG
      UXFUYDUYHUJUWBUWKUYHUWFUYAUXDUYFUYHWAUYGWEWCUWHUYHDUJZUXFUWHFWFWGWHSZUWDF
      UPZUTZDUWDSZUWFWFSZUYIUWBUYJUWGUWBFWISUYJCFJWJFWKVGVJUWBUYLUWGUWBUWDEUPZU
      TUYLUWDGUYOUXOLWLUWBUYOUYKUWDCEFJIWMVHWNVJUWHDGSZUWIUYMUWBUYPUWGCDFGGUYOC
      WOVTZUPLEUYQIWPWQZJMWRZVJUWSDGHWSZWTUWGUYNUWBUWGUWFWISUYNUWFXAUWFXBVGUSDF
      UWFUYKUWDUYKWAMUWFWAXCXDVJXEUXCUYEBUYBUXDUWJUYBUJUXBUYDDUWJUYBUWKUWFXFXGX
      HXIXJUXAUXEUXCBUWDUKZUWNUXAUXCBUXDUWDUWHUXDUWDUJUWTUXSVJXKUWTVUAUWNXLUWHU
      WTUXCUWMBUWDUWTUWJUWDSZUFUXBUWLDVUBUWTUXBUWLUJUWJUWKUWDUWDFXMXNXGXOUSXPXQ
      XRXSUWQNOPDQUWFUWDUWDXTSZUWQGXTSVUCGUYOXTLEEUYQXTICWOYAYBYCYBGUWCXTYDYEVO
      UWQUWFUWEYFZNUHZOUHZUWFUIZUWDSZOUWDULNUWDULUWEUWDUWFVDZUWQFUXLYFZUXMVUDUW
      BVUJUWPUWBUXQVUJUXRUXLGFYGVGVJUXPUXLUWEFYHYIUWQVUHNOUWDUWDUWQVUEUWDSZVUFU
      WDSZUFZUFZVUGVUEVUFFUIZUWDVUMVUGVUOUJZUWQVUEVUFUWDUWDFXMZUSVUNVUOGSZVUOHU
      GZVUOUWDSZUWBVUMVURUWPVUMUWBVUEGSZVUFGSZUFVURVUKVVAVULVVBVUEGUWCYJZVUFGUW
      CYJZYKUWBVVAVVBVURVUEVUFCEFGIJLYLYMYNYOUWBUWOVUMVUSUWIUWBVULUWOVUKVUSUWOV
      UKUFUWBVULUFZUWJVUEFUIZDUJZBUWDUKZVUKUFVUSVUKUWOVVHUWNVVHAVUEUWDUWKVUEUJZ
      UWMVVGBUWDVVIUWLVVFDUWKVUEUWJFYPXGYQZYRYSVVEVUKVVHVUSUWBVUKVVHUFZVULVUSVU
      LUWBVVKUFZVVBVUFHUGZUFVUSVUFGHWSVVLVVBVVMVUSVVLVVBUFVUOHVUFHVVLVVBVUOHUJV
      UFHUJYTZUWBVUKVVHVVBVVNYTZVUKUWBVVAVVHVVOVVCVVHUWBVVAVVGBGUKZVVOUXNVVHVVP
      YTUXOVVGBUWDGUUAYEVUEVUFCDEFGHBIJKLMUUBUUCUUDYMUUEUUFUUGUUHUUIUUJYNUUKUUL
      VUOGHWSWTZUUMUUNNOUWDUWDUWDUWFUUOWTZUWQVUKVULPUHZUWDSZUUPZUFZVUOVVSFUIZVU
      EVUFVVSFUIZFUIZVUGVVSUWFUIZVUEVUFVVSUWFUIZUWFUIZUWBVWAVWCVWEUJZUWPVWAUWBV
      VAVVBVVSGSZUUPVWIVUKVVAVULVVBVVTVWJVVCVVDVVSGUWCYJUUQVUEVUFVVSCEFGIJLUURY
      NYOVWBVWFVUOVVSUWFUIZVWCVWBVUGVUOVVSUWFVWAVUPUWQVUKVULVUPVVTVUQUUSUSUUTVW
      BVUTVVTVWKVWCUJUWQVUKVULVUTVVTVVQUVAUWQVUKVULVVTUVBVUOVVSUWDUWDFXMXIXEVWB
      VWHVUEVWGFUIZVWEVWBVUKVWGUWDSZVWHVWLUJUWQVUKVULVVTUVCUWQVUIVWAVWMVVRVUIVU
      LVVTVWMVUKVUFVVSUWDUWDUWDUWFUVDUVEUVFVUEVWGUWDUWDFXMXIVWBVWGVWDVUEFVWAVWG
      VWDUJZUWQVULVVTVWNVUKVUFVVSUWDUWDFXMUVGUSUVHXEUVIUWBUWIUYMUWOUWBUWIUFZUYP
      UWIUFUYMUWBUYPUWIUYSUVJUYTUVKZUVLUWBUWIVUKDVUEUWFUIZVUEUJUWOVWOVUKUFVWQDV
      UEFUIZVUEVWOUYMVUKVWQVWRUJVWPDVUEUWDUWDFXMUVFUWBVUKVWRVUEUJZUWIVUKUWBVVAV
      WSVVCVUECDFGJUYRMUVMYNYOXEUVNUWBUWOVUKQUHZVUEUWFUIZDUJZQUWDUKZUWIUWOVUKVX
      CUWBVUKUWOVXCVUKUWOVVHVXCUWNVVHAVUEUWDVVJUVOVVHVUKVWTVUEFUIZDUJZQUWDUKZVX
      CVVGVXEBQUWDUWJVWTUJVVFVXDDUWJVWTVUEFXFXGUVPVUKVXCVXFVUKVXBVXEQUWDVWTUWDS
      ZVUKVXBVXEXLVXGVUKUFVXAVXDDVWTVUEUWDUWDFXMXGXNXOVRUUHXJXNWCUULUVQUVRUVSUV
      T $.
      $( [8-Jun-2010] $)

    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    isdivrng3 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
                        A. x e. ( X \ { Z } ) E. y e. X ( y H x ) = U ) ) ) $=
      ( cdrng wcel cring wne cv co wceq csn cdif wrex wral wa isdivrng2 wb
      eldifi wss wi difss ssrexv ax-mp neeq1 biimparc ringolz opreq1 eqeq1d
      syl5ibrcom necon3d imp sylan2 an4s anassrs pm3.2 syl5com eldifsn syl6ibr
      imdistanda ancom 3imtr4g reximdv2 impbid2 ralbidva pm5.32da pm5.32i
      bitri ) CNOCPOZDHQZBRZARZFSZDTZBGHUAZUBZUCZAWEUDZUEZUEVRVSWCBGUCZAWEUDZUE
      ZUEABCDEFGHIJKLMUFVRWHWKVRVSWGWJVRVSUEZWFWIAWEWAWEOWLWAGOZWFWIUGWAGWDUHWL
      WMUEZWFWIWEGUIWFWIUJGWDUKWCBWEGULUMWNWCWCBGWEWNWCVTGOZUEWCVTWEOZUEWOWCUEW
      PWCUEWNWCWOWPWNWCUEZWOWOVTHQZUEZWPWQWRWOWSWLWMWCWRVRWMVSWCWRVSWCUEVRWMUEZ
      WBHQZWRWCXAVSWBDHUNUOWTXAWRWTVTHWBHWTWBHTVTHTZHWAFSZHTWACEFGHKLIJUPXBWBXC
      HVTHWAFUQURUSUTVAVBVCVDWOWRVEVFVTGHVGVHVIWOWCVJWPWCVJVKVLVMVBVNVOVPVQ $.
      $( [10-Jun-2010] $)
  $}

