$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Isometries
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$c Ismty $.
$( Extend class notation with the class of metric space isometries. $)
cismty $a class Ismty $.

${
  $d m n z f x y $.
  $( Define a function which takes two metric spaces and returns the set of
  isometries between the spaces. An isometry is a bijection which preserves
  distance. $)
  df-ismty $a |- Ismty = { <. <. m , n >. , z >. | ( ( m e. Met /\ n e. Met )
        /\ z = { f | ( f : dom dom m -1-1-onto-> dom dom n /\ A. x e. dom dom m
            A. y e. dom dom m ( x m y ) = ( ( f ` x ) n ( f ` y ) ) ) } ) } $.
$}

${
  $d M m n z f x y $. $d N m n z f x y $.
  $d X m n z f x y $. $d Y m n z f x y $.
  ismtyval.1 $e |- X = dom dom M $.
  ismtyval.2 $e |- Y = dom dom N $.
  $( The set of isometries between two metric spaces. $)
  ismtyval $p |- ( ( M e. Met /\ N e. Met ) -> ( M Ismty N ) =
                    { f | ( f : X -1-1-onto-> Y /\ A. x e. X A. y e. X
                          ( x M y ) = ( ( f ` x ) N ( f ` y ) ) ) } ) $=
    ( vm vn vz cme wcel cv wf1o co cfv wceq wral wa cab cvv cismty wf wss
    ssexg f1of adantr ss2abi mapex cdm dmexg syl syl5eqel syl2an sylancr wb
    dmeq dmeqd syl6eqr f1oeq2 opreq eqeq1d raleq12d anbi12d abbidv f1oeq3
    eqeq2d 2ralbidv df-ismty oprabval2g mpd3an3 ) DMNZEMNZFGCOZPZAOZBOZDQZVRVPR
    ZVSVPRZEQZSZBFTAFTZUAZCUBZUCNZDEUDQWGSWGFGVPUEZCUBZUFWJUCNZWHVNVOUAWGWJUCUG
    WFWICVQWIWEFGVPUHUIUJFUCNGUCNWKVNVOFGUCUCCUKVNDULZULZUCFVNWLUCNWMUCNDMUMWLU
    CUMUNHUOVOEULZULZUCGVOWNUCNWOUCNEMUMWNUCUMUNIUOUPUQJKLDEMMJOZULZULZKOZULZUL
    ZVPPZVRVSWPQZWAWBWSQZSZBWRTZAWRTZUAZCUBWGUDFXAVPPZVTXDSZBFTZAFTZUAZCUBUCWPD
    SZXHXMCXNXBXIXGXLXNWRFSXBXIURXNWRWMFXNWQWLWPDUSUTHVAZWRFXAVPVBUNXNXFXKAWRFX
    OXNXEXJBWRFXOXNXCVTXDVRVSWPDVCVDVEVEVFVGWSESZXMWFCXPXIVQXLWEXPXAGSXIVQURXPX
    AWOGXPWTWNWSEUSUTIVAXAGFVPVHUNXPXJWDABFFXPXDWCVTWAWBWSEVCVIVJVFVGABLCJKVKVL
    VM $.
    $( [2-Sep-2009] $)
$}

${
  $d M f x y $. $d N f x y $. $d X f x y $. $d Y f x y $. $d F f x y $.
  isismty.1 $e |- X = dom dom M $.
  isismty.2 $e |- Y = dom dom N $.
  $( The condition "is an isometry". $)
  isismty $p |- ( ( M e. Met /\ N e. Met ) -> ( F e. ( M Ismty N ) <->
                        ( F : X -1-1-onto-> Y /\ A. x e. X A. y e. X
                            ( x M y ) = ( ( F ` x ) N ( F ` y ) ) ) ) ) $=
    ( vf cme wcel wa cismty co cv wf1o cfv wceq wral cab ismtyval eleq2d cvv
    wi wb wf fex f1of cdm dmexg syl syl5eqel syl2an expcom adantr adantrd
    f1oeq1 fveq1 opreq12d eqeq2d 2ralbidv anbi12d elab3g bitrd ) DKLZEKLZMZCDEN
    OZLCFGJPZQZAPZBPZDOZVLVJRZVMVJRZEOZSZBFTAFTZMZJUAZLZFGCQZVNVLCRZVMCRZEOZSZB
    FTAFTZMZVHVIWACABJDEFGHIUBUCVHWICUDLZUEWBWIUFVHWCWJWHVFWCWJUEVGWCVFWJFGCUGF
    UDLWJWCVFFGUDCUHFGCUIVFDUJZUJZUDFVFWKUDLWLUDLDKUKWKUDUKULHUMUNUOUPUQVTWIJCU
    DVJCSZVKWCVSWHFGVJCURWMVRWGABFFWMVQWFVNWMVOWDVPWEEVLVJCUSVMVJCUSUTVAVBVCVDU
    LVE $.
    $( [2-Sep-2009] $)
$}

${
  $d M u v x y $. $d N u v x y $. $d F u v x y $.
  $( The inverse of an isometry is an isometry. $)
  ismtycnv $p |- ( ( M e. Met /\ N e. Met ) ->
                    ( F e. ( M Ismty N ) -> `' F e. ( N Ismty M ) ) ) $=
    ( vx vy vu vv cme wcel wa cdm wf1o cv co cfv wceq wral ccnv cismty wi
    f1ocnv adantr f1ocnvdm ex anim12d imdistani opreq1 fveq2 opreq1d eqeq12d
    opreq2 opreq2d rcla42v impcom adantll syl f1ocnvfv2 adantrr adantrl
    opreq12d adantlr eqtr2d r19.21aivv jca a1i eqid isismty wb ancoms 3imtr4d
    ) BHIZCHIZJZBKKZCKKZALZDMZEMZBNZVQAOZVRAOZCNZPZEVNQDVNQZJZVOVNARZLZFMZGMZCN
    ZWHWFOZWIWFOZBNZPZGVOQFVOQZJZABCSNIWFCBSNIZWEWPTVMWEWGWOVPWGWDVNVOAUAUBWEWN
    FGVOVOWEWHVOIZWIVOIZJZWNWEWTJZWMWKAOZWLAOZCNZWJXAWEWKVNIZWLVNIZJZJWMXDPZWEW
    TXGVPWTXGTWDVPWRXEWSXFVPWRXEVNVOWHAUCUDVPWSXFVNVOWIAUCUDUEUBUFWDXGXHVPXGWDX
    HWCXHWKVRBNZXBWACNZPDEWKWLVNVNVQWKPZVSXIWBXJVQWKVRBUGXKVTXBWACVQWKAUHUIUJVR
    WLPZXIWMXJXDVRWLWKBUKXLWAXCXBCVRWLAUHULUJUMUNUOUPVPWTXDWJPWDVPWTJXBWHXCWICV
    PWRXBWHPWSVNVOWHAUQURVPWSXCWIPWRVNVOWIAUQUSUTVAVBUDVCVDVEDEABCVNVOVNVFZVOVF
    ZVGVLVKWQWPVHFGWFCBVOVNXNXMVGVIVJ $.
    $( [2-Sep-2009] $)
$}

${
  $d M q r s t u v w x y z $. $d N q r s t u v w x y z $.
  $d J q r s t u v w x y z $. $d K q r s t u v w x y z $.
  $d F q r s t u v w x y z $.
  ismtyhmeo.1 $e |- J = ( Open ` M ) $.
  ismtyhmeo.2 $e |- K = ( Open ` N ) $.

  $( Lemma for ~ ismtyhmeo . $)
  ismtyhmeolem $p |- ( ( ( M e. Met /\ N e. Met ) /\
              ( F : dom dom M -1-1-onto-> dom dom N /\ A. x e. dom dom M
                  A. y e. dom dom M ( x M y ) = ( ( F ` x ) N ( F ` y ) ) ) )
                                            -> A. u e. J ( F " u ) e. K ) $=
    ( vv vs vw vt vz vq vr cme wcel wa cdm wf1o cv co cfv wceq wral cima wss
    cbl crn wrex wi wfun f1ofun fvelima ex syl adantr ad2antlr cc0 clt wbr cr
    opreq1 eqeq2d anbi2d rexbidv rcla4ev wf ffvelrn f1of sylan ad2ant2r
    ad2ant2lr simprlr simprrl eqid blssm simpll simprll simprr syl2anc
    adantlrr adantllr adantrrr sseld imp adantrr eleq1 syl5cbi expimpd ancomsd
    ad2antrl ad2antrr expdimp anasss mpd opreq2 breq1d fveq2 opreq1d eqeq12d
    opreq2d rcla42v com12 ad2antll mp2and wb elbl pm3.26 pm3.27bda an1rs
    eqbrtrrd impr jca expr r19.23adva ccnv eqeq1d f1ocnvdm adantll adantlr
    f1ocnvfv2 eqtrd biimpar simplr simprl mpbird sylanc impbid imaeq2 adantl
    eleq2d wfn fvelimab f1ofn bitrd adantlll 3bitr4d eqrdv breq2 anbi12d
    sylan32c exp32 r19.23advv blrn3 3imtr4d funfvima2 fdm sseq2d sstr ancoms
    sylan2 anassrs ancom2s imass2 eleq2 sseq1 exp31 elequ1 anbi1d rcla4cva
    syld r19.21aiv wfo f1ofo forn imassrn sseq2 mpbii 3syl jctild isopn ) GRSZH
    RSZTZGUAUAZHUAUAZDUBZAUCZBUCZGUDZUWADUEZUWBDUEZHUDZUFZBUVRUGAUVRUGZTZTZDCUC
    ZUHZFSZCEUWJUWKUVRUIZKUCZLUCZSZUWPUWKUIZTZLGUJUEZUKZULZKUWKUGZTZUWLUVSUIZMU
    CZNUCZSZUXGUWLUIZTZNHUJUEZUKZULZMUWLUGZTZUWKESZUWMUWJUXDUXNUXEUWJUXDUXNUWJU
    XDTZUXMMUWLUXQUXFUWLSZOUCZDUEZUXFUFZOUWKULZUXMUWIUXRUYBUMZUVQUXDUVTUYCUWHUV
    TDUNZUYCUVRUVSDUOZUYDUXRUYBOUXFUWKDUPUQURUSUTUWJUWNUXCUYBUXMUMUWJUWNTZUXCTU
    YAUXMOUWKUYFUXCUXSUWKSZUYAUXMUMZUYFUXSUWPSZUWRTZLUXAULZUYHUXCUYGTUYFUYKUYHU
    YFUYJUYHLUXAUYFUWPUXASZTZUYJUYAUXMDUWPUHZUXLSZUXFUYNSZUYNUWLUIZUXMUYMUYJTZU
    YATUYMUYOUYJUYAUWJUYLUYOUWNUWJUYLUYOUWJVAPUCZVBVCZUWPUWOUYSUWTUDZUFZTZPVDUL
    KUVRULZVAQUCZVBVCZUYNUXFVUEUXKUDZUFZTZQVDULZMUVSULZUYLUYOUWJVUCVUKKPUVRVDUW
    JUWOUVRSZUYSVDSZTZVUCVUKUWODUEZUVSSZVUFUYNVUOVUEUXKUDZUFZTZQVDULZVUKUWJVUNV
    UCTZTZVUJVUTMVUOUVSUXFVUOUFZVUIVUSQVDVVCVUHVURVUFVVCVUGVUQUYNUXFVUOVUEUXKVE
    VFVGVHVIUWIVUNVUPUVQVUCUVTVULVUPUWHVUMUVRUVSDVJZVULVUPUVTUVRUVSUWODVKUVRUVS
    DVLZVMZVNVOVUMUYTUYNVUOUYSUXKUDZUFZVUTVVBUWJVULVUMVUCVPUWJVUNUYTVUBVQVVBOUY
    NVVGVVBUXFDUEZUXSUFZMVUAULZUXSUVSSZVUOUXSHUDZUYSVBVCZTZUXSUYNSZUXSVVGSZVVBV
    VKVVOVVBVVJVVOMVUAVVBUXFVUASZVVJVVOVVBVVRVVJTTZVVLVVNVVSUXFUVRSZVVLVVBVVRVV
    TVVJVVBVVRVVTVVBVUAUVRUXFUWJVUNUYTVUAUVRUIZVUBUVOUWIVUNUYTTZVWAUVPUVOUVTVWB
    VWAUWHUVOVULVUMUYTVWAUVOUVTTZVWBTZGUWOUYSUVRUVRVRZVSUVOUVTVWBVTVWCVULVUMUYT
    WAVWCVULVUMUYTVPVWCVUNUYTWBWCZWDWEWFWGWHZWIVVBVVRVVJVVTVVLUMVVBVVRTZVVJVVTV
    VLUWJVVJVVTTVVLUMZVVAVVRUVTVWIUVQUWHUVTVVTVVJVVLUVTVVTVVJVVLVVJVVIUVSSZVVLU
    VTVVTTVVIUXSUVSWJVVDVVTVWJUVTUVRUVSUXFDVKVVEVMWKWLWMWNWOWPWQWRVVBVVRVVJVVNV
    VJVUOVVIHUDZUYSVBVCVVNVWHVVJVWKVVMUYSVBVVIUXSVUOHWSWTVWHUWOUXFGUDZVWKUYSVBV
    WHVULVVTVWLVWKUFZVVAVULUWJVVRVULVUMVUCVTZUTVWGUWJVULVVTTZVWMUMZVVAVVRUWHVWP
    UVQUVTVWOUWHVWMUWGVWMUWOUWBGUDZVUOUWEHUDZUFZABUWOUXFUVRUVRUWAUWOUFZUWCVWQUW
    FVWRUWAUWOUWBGVEVWTUWDVUOUWEHUWAUWODXAXBXCZUWBUXFUFZVWQVWLVWRVWKUWBUXFUWOGW
    SVXBUWEVVIVUOHUWBUXFDXAXDXCXEXFXGWOXHUVQVVAVVRVWLUYSVBVCZUWIUVOVVAVVRVXCUVP
    UVOVVRVVAVXCUVOVVRTVUNUYTVXCVUBUVOVWBVVRVXCUVOVWBTZVVRVVTVXCUVOVULVUMUYTVVR
    VVTVXCTXIVXDUXFGUWOUYSUVRVWEXJUVOVWBXKUVOVULVUMUYTWAUVOVULVUMUYTVPUVOVUNUYT
    WBWCXLXMWFXMWEWEXNWKXOXPXQXRVVBVVOVVKUXSDXSUEZVUASZVXEDUEZUXSUFZVVKVVBVVOTZ
    VVJVXHMVXEVUAUXFVXEUFVVIVXGUXSUXFVXEDXAXTVIVXIVXFVXEUVRSZUWOVXEGUDZUYSVBVCZ
    TZVXIVXJVXLUWJVVLVXJVVAVVNUVQUVTVVLVXJUWHUVTVVLVXJUVQUVRUVSUXSDYAYBWDZVNVVB
    VVLVVNVXLVVBVVLTZVXLVVNVXOVXKVVMUYSVBVXOVXKVUOVXGHUDZVVMVXOVULVXJVXKVXPUFZV
    VAVULUWJVVLVWNUTUWJVVLVXJVVAVXNYCUWJVULVXJTZVXQUMZVVAVVLUWHVXSUVQUVTVXRUWHV
    XQUWGVXQVWSABUWOVXEUVRUVRVXAUWBVXEUFZVWQVXKVWRVXPUWBVXEUWOGWSVXTUWEVXGVUOHU
    WBVXEDXAXDXCXEXFXGWOXHVXOVXGUXSVUOHUWJVVLVXHVVAUVQUVTVVLVXHUWHUVTVVLVXHUVQU
    VRUVSUXSDYDYBWDZYCXDYEWTYFWQXPUVOVULVUMUYTVXFVXMXIVXIVXEGUWOUYSUVRVWEXJUWJU
    VOVVAVVOUVOUVPUWIVTWOVVAVULUWJVVOVWNUTVVAVUMUWJVVOVULVUMVUCYGUTVVAUYTUWJVVO
    VUNUYTVUBYHUTWCYIUWJVVLVXHVVAVVNVYAVNYJUQYKVVBVVPUXSDVUAUHZSZVVKVVBUYNVYBUX
    SVUCUYNVYBUFZUWJVUNVUBVYDUYTUWPVUADYLYMXGYNUWJVUNUYTVYCVVKXIZVUBUVQUVTVWBVY
    EUWHUVOUVTVWBVYEUVPDUVRYOZVWAVYEVWDMUVRVUAUXSDYPUVTVYFUVOVWBUVRUVSDYQUTVWFY
    JWEWDWFYRUWJVUNUYTVVQVVOXIZVUBUVQUVTVWBVYGUWHUVPUVTVWBVYGUVOUVPVUPVUMUYTVYG
    UVPUVTTZVWBTUXSHVUOUYSUVSUVSVRZXJUVPUVTVWBVTUVTVUNVUPUVPUYTUVTVULVUPVUMVVFW
    IVOVYHVULVUMUYTVPVYHVUNUYTWBWCYSWDWFYTUUAVUSUYTVVHTQUYSVDVUEUYSUFZVUFUYTVUR
    VVHVUEUYSVAVBUUBVYJVUQVVGUYNVUEUYSVUOUXKWSVFUUCVIUUDYJUUEUUFUVOUYLVUDXIUVPU
    WIKPUWPGUVRVWEUUGWOUVPUYOVUKXIUVOUWIMQUYNHUVSVYIUUGUTUUHWHYCWOUYRUYAUYPUYAU
    XTUYNSZUYPUYRUXTUXFUYNWJUYMUWRUYIVYKUYMUWRUYIVYKUYFUWRUYIVYKUMZUYLUWJUWNUWR
    VYLUWJUWPUVRUIZVYLUWNUWRTUWIVYMVYLUVQUVTVYMVYLUWHUYDUWPDUAZUIZVYLUVTVYMTUWP
    UXSDUUIUVTUYDVYMUYEUSUVTVYOVYMUVTVYNUVRUWPUVTVVDVYNUVRUFVVEUVRUVSDUUJURUUKY
    FYJYCYBUWRUWNVYMUWPUWKUVRUULUUMUUNUUOYCXOUUPWKWHUYJUYQUYMUYAUWRUYQUYIUWPUWK
    DUUQYMUTUXJUYPUYQTNUYNUXLUXGUYNUFUXHUYPUXIUYQUXGUYNUXFUURUXGUYNUWLUUSUUCVIU
    UDUUTXRWHUXBUYKKUXSUWKUWOUXSUFZUWSUYJLUXAVYPUWQUYIUWRKOLUVAUVBVHUVCUUNUUOXR
    WQUVDUVEUQUVTUXEUVQUWHUVTUVRUVSDUVFDUKZUVSUFZUXEUVRUVSDUVGUVRUVSDUVHVYRUWLV
    YQUIUXEDUWKUVIVYQUVSUWLUVJUVKUVLWNUVMUVOUXPUXDXIUVPUWIKLUWKGEUVRVWEIUVNWOUV
    PUWMUXOXIUVOUWIMNUWLHFUVSVYIJUVNUTUUHUVE $.
    $( [2-Sep-2009] $)

  $( An isometry is a homeomorphism on the induced topology. $)
  ismtyhmeo $p |- ( ( M e. Met /\ N e. Met ) ->
                          ( F e. ( M Ismty N ) -> F e. ( J Homeo K ) ) ) $=
    ( vx vy vu vs vt cme wcel wa cdm wf1o cv co cfv wceq wral cuni cima ccnv
    w3a cismty chomeosm wb eqid uniopn2 eqcomd adantr f1oeq2 syl adantl f1oeq3
    bitrd biimpd adantrd ismtyhmeolem ex f1ocnv wi f1ocnvdm anim12d imdistani
    opreq1 fveq2 opreq1d eqeq12d opreq2 opreq2d rcla42v impcom adantll
    f1ocnvfv2 adantrr adantrl opreq12d adantlr eqtr2d r19.21aivv jca sylan2
    ancom1s 3jcad isismty ctop ishomeo2 opntop syl2an 3imtr4d ) DMNZEMNZOZDPPZE
    PPZAQZHRZIRZDSZWTATZXAATZESZUAZIWQUBHWQUBZOZBUCZCUCZAQZAJRZUDCNJBUBZAUEZXLU
    DBNJCUBZUFZADEUGSNABCUHSNZWPXHXKXMXOWPWSXKXGWPWSXKWPWSXIWRAQZXKWPWQXIUAZWSX
    RUIWNXSWOWNXIWQDBWQWQUJZFUKULUMWQXIWRAUNUOWPWRXJUAZXRXKUIWOYAWNWOXJWRECWRWR
    UJZGUKULUPWRXJXIAUQUOURUSUTWPXHXMHIJABCDEFGVAVBWPXHXOWOWNXHXOWOWNOWRWQXNQZK
    RZLRZESZYDXNTZYEXNTZDSZUAZLWRUBKWRUBZOXOXHKLJXNCBEDGFVAXHYCYKWSYCXGWQWRAVCU
    MXHYJKLWRWRXHYDWRNZYEWRNZOZYJXHYNOZYIYGATZYHATZESZYFYOXHYGWQNZYHWQNZOZOYIYR
    UAZXHYNUUAWSYNUUAVDXGWSYLYSYMYTWSYLYSWQWRYDAVEVBWSYMYTWQWRYEAVEVBVFUMVGXGUU
    AUUBWSUUAXGUUBXFUUBYGXADSZYPXDESZUAHIYGYHWQWQWTYGUAZXBUUCXEUUDWTYGXADVHUUEX
    CYPXDEWTYGAVIVJVKXAYHUAZUUCYIUUDYRXAYHYGDVLUUFXDYQYPEXAYHAVIVMVKVNVOVPUOWSY
    NYRYFUAXGWSYNOYPYDYQYEEWSYLYPYDUAYMWQWRYDAVQVRWSYMYQYEUAYLWQWRYEAVQVSVTWAWB
    VBWCWDWEWFVBWGHIADEWQWRXTYBWHBWINCWINXQXPUIWNWOJABCXIXJXIUJXJUJWJDBFWKECGWK
    WLWM $.
    $( [2-Sep-2009] $)
$}

${
  $d M r u v x y z $. $d N r u v x y z $. $d F r u v x y z $.
  $( Lemma for ~ ismtybnd . $)
  ismtybndlem $p |- ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) )
                                            -> ( M e. Bnd -> N e. Bnd ) ) $=
    ( vx vr vy vu vv vz cme wcel cismty co w3a cdm cv cbl cfv wceq crp wrex
    wral cbnd wi wa wf1o eqid isismty ccnv f1ocnvdm adantlr adantll clt wbr ex
    anim12d adantr imdistani opreq1 fveq2 opreq1d eqeq12d opreq2 opreq2d
    rcla42v impcom syl anassrs adantlrr f1ocnvfv2 ad2ant2r ad2antrr adantllr
    adantlll opreq12d eqtr2d eleq2 syl5cbi imp an1rs cr cc0 wb elbl2 simplll
    3jca rpregt0 ad2antll sylanc mpbid eqbrtrd simpllr simprl pm3.27 mpbird
    ssrdv wss blssm elrp sylan2b anasss eqssd r19.22dva eqeq2d rexbidv
    rcla4cva syl5 expdimp mpd r19.21aiva exp31 sylbid 3impia isbnd2 3ad2ant1
    3ad2ant2 3imtr4d ) BJKZCJKZABCLMKZNBOOZDPZEPZBQRZMZSZETUAZDYAUBZCOOZFPZYCCQ
    RMZSZETUAZFYIUBZBUCKZCUCKZXRXSXTYHYNUDZXRXSUEZXTYAYIAUFZGPZHPZBMZYTARZUUAAR
    ZCMZSZHYAUBGYAUBZUEZYQGHABCYAYIYAUGZYIUGZUHYRUUHYHYNYRUUHUEZYHUEZYMFYIUULYJ
    YIKZUEYJAUIZRZYAKZYMUUKUUMUUPYHUUHUUMUUPYRYSUUMUUPUUGYAYIYJAUJZUKULUKUUKUUM
    YHUUPYMUDUUKUUMUEZYHUUPYMUURYAUUOYCYDMZSZETUAZYMYHUUPUEUURUUTYLETUUKUUMYCTK
    ZUUTYLUDUUKUUMUVBUEZUEZUUTYLUVDUUTUEZYIYKUVEIYIYKUVEIPZYIKZUVFYKKZUVEUVGUEZ
    UVHYJUVFCMZYCUMUNZUVIUVJUUOUVFUUNRZBMZYCUMUVIUVMUUOARZUVLARZCMZUVJUVDUVGUVM
    UVPSZUUTUUKUUMUVGUVQUVBUUKUUMUVGUVQUUHUUMUVGUEZUVQYRUUHUVRUEUUHUUPUVLYAKZUE
    ZUEUVQUUHUVRUVTYSUVRUVTUDUUGYSUUMUUPUVGUVSYSUUMUUPUUQUOYSUVGUVSYAYIUVFAUJZU
    OUPUQURUUGUVTUVQYSUVTUUGUVQUUFUVQUUOUUABMZUVNUUDCMZSGHUUOUVLYAYAYTUUOSZUUBU
    WBUUEUWCYTUUOUUABUSUWDUUCUVNUUDCYTUUOAUTVAVBUUAUVLSZUWBUVMUWCUVPUUAUVLUUOBV
    CUWEUUDUVOUVNCUUAUVLAUTVDVBVEVFULVGULVHVIUKUVIUVNYJUVOUVFCUVDUVNYJSZUUTUVGU
    UHUVCUWFYRYSUUMUWFUUGUVBYAYIYJAVJVKULVLUVDUVGUVOUVFSZUUTUUHUVCUVGUWGYRYSUVC
    UVGUWGUUGYSUVGUWGUVCYAYIUVFAVJUKVMVNUKVOVPUVIUVLUUSKZUVMYCUMUNZUVDUVGUUTUWH
    UVDUVGUEZUUTUWHUUTUVSUWHUWJYAUUSUVLVQUUKUVGUVSUVCUUHUVGUVSYRYSUVGUVSUUGUWAU
    KULUKZVRVSVTXRUUPUVSNYCWAKWBYCUMUNUEZUWHUWIWCUVIUVLBUUOYCYAUUIWDUVIXRUUPUVS
    UVDXRUUTUVGXRXSUUHUVCWEVLUVDUUPUUTUVGUUHUVCUUPYRYSUUMUUPUUGUVBUUQVKULVLUVDU
    VGUVSUUTUWKUKWFUVDUWLUUTUVGUVBUWLUUKUUMYCWGWHVLZWIWJWKXSUUMUVGNUWLUVHUVKWCU
    VIUVFCYJYCYIUUJWDUVIXSUUMUVGUVDXSUUTUVGXRXSUUHUVCWLVLUVDUUMUUTUVGUUKUUMUVBW
    MVLUVEUVGWNWFUWMWIWOUOWPUVDYKYIWQZUUTYRUVCUWNUUHXSUVCUWNXRXSUUMUVBUWNXSUUMU
    EUWLUWNUVBCYJYCYIUUJWRYCWSWTXAULUKUQXBUOVHXCYGUVADUUOYAYBUUOSZYFUUTETUWOYEU
    USYAYBUUOYCYDUSXDXEXFXGXHVTXIXJXKXLXMXRXSYOYHWCXTDBYAEUUIXNXOXSXRYPYNWCXTFC
    YIEUUJXNXPXQ $.
    $( [2-Sep-2009] $)

  $( Isometries preserve boundedness. $)
  ismtybnd $p |- ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) )
                                            -> ( M e. Bnd <-> N e. Bnd ) ) $=
    ( cme wcel cismty co w3a cbnd ismtybndlem wi wa ccnv ismtycnv 3expia
    ancoms syld 3impia impbid ) BDEZCDEZABCFGEZHBIEZCIEZABCJTUAUBUDUCKZTUALUBAM
    ZCBFGEZUEABCNUATUGUEKUATUGUEUFCBJOPQRS $.
    $( [2-Sep-2009] $)
$}

${
  $d X u v x y $. $d M u v x y $. $d N u v x y $. $d A u v x y $.
  $d B u v x y $. $d S u v x y $. $d T u v x y $. $d F u v x y $.
  ismtyres.1 $e |- X = dom dom M $.
  ismtyres.2 $e |- B = ( F " A ) $.
  ismtyres.3 $e |- S = ( M |` ( A X. A ) ) $.
  ismtyres.4 $e |- T = ( N |` ( B X. B ) ) $.
  $( A restriction of an isometry is an isometry. The condition ` A (_ X `
  is not necessary but makes the proof easier. $)
  ismtyres $p |- ( ( ( M e. Met /\ N e. Met ) /\ ( F e. ( M Ismty N )
                            /\ A (_ X ) ) -> ( F |` A ) e. ( S Ismty T ) ) $=
    ( vx vy vu vv cme wcel wa wss cismty co cres cdm wf1o cv cfv wceq wral wf1
    cima f1ores ancoms adantll wb basmetres f1oeq2 syl adantlr adantr cxp eqid
    metssba2 wf crn imassrn a1i frn sstrd adantl f1f sylan2 anassrs xpeq1i
    xpeq2i eqtri reseq2 ax-mp dmeqi syl6eqr f1oeq3 adantlll bitrd mpbid ex
    f1of1 syl5 adantrd eleq2d anbi12d wi ssel anim12d imp opreq1 fveq2 opreq1d
    eqeq12d opreq2 opreq2d rcla42v an1rs adantlrl oprvres opreqi syl5eq fvres
    ad2antrl ad2antll opreq12d wfun funfvima2 f1ofun f1of fdm sseq2d biimparc
    sylanc syl6eleqr adantrr adantrl eqtrd adantlrr 3eqtr4d sylbird r19.21aivv
    jcad isismty metres syl5eqel syl2an 3imtr4d impr ancom2s ) FQRZGQRZSZAHTZEF
    GUAUBRZEAUCZCDUAUBRZYQYRYSUUAYQYRSZHGUDUDZEUEZMUFZNUFZFUBZUUEEUGZUUFEUGZGUB
    ZUHZNHUIMHUIZSZCUDUDZDUDZUDZYTUEZOUFZPUFZCUBZUURYTUGZUUSYTUGZDUBZUHZPUUNUIO
    UUNUIZSZYSUUAUUBUUMUUQUVEUUBUUDUUQUULUUBHUUCEUJZUUQUUDUUBUVGUUQUUBUVGSZAEAU
    KZYTUEZUUQYRUVGUVJYQUVGYRUVJHUUCAEULUMUNUVHUVJUUNUVIYTUEZUUQUUBUVJUVKUOZUVG
    YOYRUVLYPYOYRSZAUUNUHUVLFCHAKIUPZAUUNUVIYTUQURUSUTYPYRUVGUVKUUQUOZYOYPYRSUV
    GSZUVIUUPUHUVOUVPUVIGUVIUVIVAZUCZUDZUDZUUPYPYRUVGUVIUVTUHZYPUVIUUCTZUWAYRUV
    GSGUUCUVIUUCVBZVCYRHUUCEVDZUWBUVGUWDUWBYRUWDUVIEVEZUUCUVIUWETUWDEAVFVGHUUCE
    VHVIVJHUUCEVKVLVLVMUUOUVSDUVRDGBBVAZUCZUVRLUWFUVQUHUWGUVRUHUWFUVIBVAUVQBUVI
    BJVNBUVIUVIJVOVPUWFUVQGVQVRVPVSVSVTUVIUUPUUNYTWAURWBWCWDWEHUUCEWFWGWHUUBUUM
    UVEUUBUUMSZUVDOPUUNUUNUWHUURUUNRZUUSUUNRZSZUURARZUUSARZSZUVDUUBUWNUWKUOZUUM
    YOYRUWOYPUVMUWLUWIUWMUWJUVMAUUNUURUVNWIUVMAUUNUUSUVNWIWJUSUTUWHUWNUVDUWHUWN
    SUURUUSFUBZUUREUGZUUSEUGZGUBZUUTUVCYRUUMUWNUWPUWSUHZYQYRUULUWNUWTUUDYRUWNUU
    LUWTYRUWNSZUULUWTUXAUURHRZUUSHRZSZUULUWTWKYRUWNUXDYRUWLUXBUWMUXCAHUURWLAHUU
    SWLWMWNUUKUWTUURUUFFUBZUWQUUIGUBZUHMNUURUUSHHUUEUURUHZUUGUXEUUJUXFUUEUURUUF
    FWOUXGUUHUWQUUIGUUEUUREWPWQWRUUFUUSUHZUXEUWPUXFUWSUUFUUSUURFWSUXHUUIUWRUWQG
    UUFUUSEWPWTWRXAURWNXBXCWBUWNUUTUWPUHUWHUWNUURUUSFAAVAUCZUBUWPUUTUURUUSAAFXD
    CUXIUURUUSKXEXFVJYRUUMUWNUVCUWSUHZYQYRUUDUWNUXJUULYRUUDSZUWNSZUVCUWQUWRDUBZ
    UWSUXLUVAUWQUVBUWRDUWLUVAUWQUHUXKUWMUURAEXGXHUWMUVBUWRUHUXKUWLUUSAEXGXIXJUX
    LUWQUWRUWGUBZUWSUXMUWQBRZUWRBRZUXNUWSUHUXLUWQUWRBBGXDUXKUWLUXOUWMUXKUWLSUWQ
    UVIBUXKUWLUWQUVIRZEXKZAEUDZTZUWLUXQWKUXKAUUREXLUUDUXRYRHUUCEXMVJZUUDUXTYRUU
    DUXSHAUUDUWDUXSHUHHUUCEXNHUUCEXOURXPXQZXRWNJXSXTUXKUWMUXPUWLUXKUWMSUWRUVIBU
    XKUWMUWRUVIRZUXRUXTUWMUYCWKUXKAUUSEXLUYAUYBXRWNJXSYAXRDUWGUWQUWRLXEXFYBYCWB
    YDWEYEYFWEYGYQYSUUMUOYRMNEFGHUUCIUWCYHUTYQUUAUVFUOZYRCQRDQRUYDYOYPOPYTCDUUN
    UUPUUNVBUUPVBYHYOUXIQCFAYIKYJYPUWGQDGBYILYJYKUTYLYMYN $.
    $( [2-Sep-2009] $)
$}

