$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Isometries
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Ismty $.
  $( Extend class notation with the class of metric space isometries. $)
  cismty $a class Ismty $.

  ${
    $d m n z f x y $.
    $( Define a function which takes two metric spaces and returns the set of
       isometries between the spaces.  An isometry is a bijection which
       preserves distance. $)
    df-ismty $a |- Ismty = { <. <. m , n >. , z >. | ( ( m e. Met /\ n e. Met )
        /\ z = { f | ( f : dom dom m -1-1-onto-> dom dom n /\ A. x e. dom dom m
            A. y e. dom dom m ( x m y ) = ( ( f ` x ) n ( f ` y ) ) ) } ) } $.
  $}

  ${
    $d M m n z f x y $.  $d N m n z f x y $.  $d X m n z f x y $.
    $d Y m n z f x y $.
    ismtyval.1 $e |- X = dom dom M $.
    ismtyval.2 $e |- Y = dom dom N $.
    $( The set of isometries between two metric spaces.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    ismtyval $p |- ( ( M e. Met /\ N e. Met ) -> ( M Ismty N ) =
                      { f | ( f : X -1-1-onto-> Y /\ A. x e. X A. y e. X
                            ( x M y ) = ( ( f ` x ) N ( f ` y ) ) ) } ) $=
      ( cme wcel cv co wceq wral wa cab cvv cdm dmexg vm vn wf1o cfv cismty wss
      vz wf f1of adantr ss2abi syl syl5eqel mapex syl2an ssexg sylancr wb dmeqd
      dmeq syl6eqr f1oeq2 opreq eqeq1d raleqbidv anbi12d abbidv f1oeq3 2ralbidv
      eqeq2d df-ismty oprabval2g mpd3an3 ) DJKZEJKZFGCLZUCZALZBLZDMZVRVPUDZVSVP
      UDZEMZNZBFOAFOZPZCQZRKZDEUEMWGNVNVOPWGFGVPUHZCQZUFWJRKZWHWFWICVQWIWEFGVPU
      IUJUKVNFRKGRKWKVOVNFDSZSZRHVNWLRKWMRKDJTWLRTULUMVOGESZSZRIVOWNRKWORKEJTWN
      RTULUMFGRRCUNUOWGWJRUPUQUAUBUGDEJJUALZSZSZUBLZSZSZVPUCZVRVSWPMZWAWBWSMZNZ
      BWROZAWROZPZCQWGUEFXAVPUCZVTXDNZBFOZAFOZPZCQRWPDNZXHXMCXNXBXIXGXLXNWRFNXB
      XIURXNWRWMFXNWQWLWPDUTUSHVAZWRFXAVPVBULXNXFXKAWRFXOXNXEXJBWRFXOXNXCVTXDVR
      VSWPDVCVDVEVEVFVGWSENZXMWFCXPXIVQXLWEXPXAGNXIVQURXPXAWOGXPWTWNWSEUTUSIVAX
      AGFVPVHULXPXJWDABFFXPXDWCVTWAWBWSEVCVJVIVFVGABUGCUAUBVKVLVM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M f x y $.  $d N f x y $.  $d X f x y $.  $d Y f x y $.  $d F f x y $.
    isismty.1 $e |- X = dom dom M $.
    isismty.2 $e |- Y = dom dom N $.
    $( The condition "is an isometry".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isismty $p |- ( ( M e. Met /\ N e. Met ) -> ( F e. ( M Ismty N ) <->
                          ( F : X -1-1-onto-> Y /\ A. x e. X A. y e. X
                              ( x M y ) = ( ( F ` x ) N ( F ` y ) ) ) ) ) $=
      ( vf cme wcel wa co cv wf1o cfv wceq wral cvv cismty ismtyval eleq2d f1of
      cab wi wb wf cdm dmexg syl syl5eqel fex syl2an expcom adantr f1oeq1 fveq1
      adantrd opreq12d eqeq2d 2ralbidv anbi12d elab3g bitrd ) DKLZEKLZMZCDEUANZ
      LCFGJOZPZAOZBOZDNZVLVJQZVMVJQZENZRZBFSAFSZMZJUEZLZFGCPZVNVLCQZVMCQZENZRZB
      FSAFSZMZVHVIWACABJDEFGHIUBUCVHWICTLZUFWBWIUGVHWCWJWHVFWCWJUFVGWCVFWJWCFGC
      UHFTLWJVFFGCUDVFFDUIZUIZTHVFWKTLWLTLDKUJWKTUJUKULFGTCUMUNUOUPUSVTWIJCTVJC
      RZVKWCVSWHFGVJCUQWMVRWGABFFWMVQWFVNWMVOWDVPWEEVLVJCURVMVJCURUTVAVBVCVDUKV
      E $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M u v x y $.  $d N u v x y $.  $d F u v x y $.
    $( The inverse of an isometry is an isometry.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    ismtycnv $p |- ( ( M e. Met /\ N e. Met ) ->
                      ( F e. ( M Ismty N ) -> `' F e. ( N Ismty M ) ) ) $=
      ( vx vy vu vv cme wcel wa cdm wf1o cv co cfv wceq wral cismty wi ex fveq2
      ccnv f1ocnv adantr f1ocnvdm anim12d opreq1 opreq1d eqeq12d opreq2 opreq2d
      imdistani rcla42v impcom adantll f1ocnvfv2 adantrr adantrl adantlr eqtr2d
      syl opreq12d ralrimivv jca a1i eqid isismty wb ancoms 3imtr4d ) BHIZCHIZJ
      ZBKKZCKKZALZDMZEMZBNZVQAOZVRAOZCNZPZEVNQDVNQZJZVOVNAUBZLZFMZGMZCNZWHWFOZW
      IWFOZBNZPZGVOQFVOQZJZABCRNIWFCBRNIZWEWPSVMWEWGWOVPWGWDVNVOAUCUDWEWNFGVOVO
      WEWHVOIZWIVOIZJZWNWEWTJZWMWKAOZWLAOZCNZWJXAWEWKVNIZWLVNIZJZJWMXDPZWEWTXGV
      PWTXGSWDVPWRXEWSXFVPWRXEVNVOWHAUETVPWSXFVNVOWIAUETUFUDULWDXGXHVPXGWDXHWCX
      HWKVRBNZXBWACNZPDEWKWLVNVNVQWKPZVSXIWBXJVQWKVRBUGXKVTXBWACVQWKAUAUHUIVRWL
      PZXIWMXJXDVRWLWKBUJXLWAXCXBCVRWLAUAUKUIUMUNUOVAVPWTXDWJPWDVPWTJXBWHXCWICV
      PWRXBWHPWSVNVOWHAUPUQVPWSXCWIPWRVNVOWIAUPURVBUSUTTVCVDVEDEABCVNVOVNVFZVOV
      FZVGVLVKWQWPVHFGWFCBVOVNXNXMVGVIVJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y F $.  $d x y M $.  $d x y N $.  $d x y P $.  $d x R $.  $d x y X $.
    ismtyima.1 $e |- X = dom dom M $.
    $( The image of a ball under an isometry is another ball. $)
    ismtyima $p |- ( ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) ) /\
                       ( P e. X /\ R e. RR+ ) ) ->
               ( F " ( P ( ball ` M ) R ) ) = ( ( F ` P ) ( ball ` N ) R ) ) $=
      ( vx vy wcel co wa cfv wss wceq adantr syl syl3anc wb clt cme w3a crp cbl
      cismty cima cv cdm crn imassrn wf1o wral eqid isismty biimp3a simpld f1of
      wf frn syl5ss sseld simpl2 simprl ffvelrn syl2anc simprr blssm wbr simpl1
      ccnv simplrr simplrl f1ocnv 3syl sylan elbl2 syl22anc simprd opreq1 fveq2
      wi opreq1d eqeq12d opreq2 opreq2d rcla42v impancom imp syldan bitrd f1of1
      breq1d wf1 f1elima f1ocnvfv2 simpr eqeltrd 3bitr4d eleq1d pm5.21ndd eqrdv
      3bitr3d ex ) DUAJZEUAJZCDEUEKJZUBZAFJZBUCJZLZLZHCABDUDMKZUFZACMZBEUDMKZXK
      HUGZEUHUHZJZXPXMJZXPXOJZXKXMXQXPXKXMCUIZXQCXLUJXKFXQCURZYAXQNXKFXQCUKZYBX
      KYCXPIUGZDKZXPCMZYDCMZEKZOZIFULHFULZXGYCYJLZXJXDXEXFYKHICDEFXQGXQUMZUNUOP
      ZUPZFXQCUQQZFXQCUSQUTVAXKXOXQXPXKXEXNXQJZXIXOXQNXDXEXFXJVBZXKYBXHYPYOXGXH
      XIVCZFXQACVDVEZXGXHXIVFZEXNBXQYLVGRVAXKXRXSXTSXKXRLZXPCVJZMZCMZXMJZUUDXOJ
      ZXSXTUUAUUCXLJZXNUUDEKZBTVHZUUEUUFUUAUUGAUUCDKZBTVHZUUIUUAXDXIXHUUCFJZUUG
      UUKSXKXDXRXDXEXFXJVIZPXGXHXIXRVKZXGXHXIXRVLXKXQFUUBURZXRUULXKYCXQFUUBUKUU
      OYNFXQCVMXQFUUBUQVNXQFXPUUBVDVOZUUCDABFGVPVQUUAUUJUUHBTXKXRUULUUJUUHOZUUP
      XKUULUUQXKXHYJUULUUQWAYRXKYCYJYMVRXHUULYJUUQYIUUQAYDDKZXNYGEKZOHIAUUCFFXP
      AOZYEUURYHUUSXPAYDDVSUUTYFXNYGEXPACVTWBWCYDUUCOZUURUUJUUSUUHYDUUCADWDUVAY
      GUUDXNEYDUUCCVTWEWCWFWGVEWHWIWLWJUUAFXQCWMZUULXLFNZUUEUUGSXKUVBXRXKYCUVBY
      NFXQCWKQPUUPXKUVCXRXKXDXHXIUVCUUMYRYTDABFGVGRPFXQCUUCXLWNRUUAXEXIYPUUDXQJ
      UUFUUISXKXEXRYQPUUNXKYPXRYSPUUAUUDXPXQXKYCXRUUDXPOYNFXQXPCWOVOZXKXRWPWQUU
      DEXNBXQYLVPVQWRUUAUUDXPXMUVDWSUUAUUDXPXOUVDWSXBXCWTXA $.
      $( [31-Jan-2014] $)
  $}

  ${
    $d u v w x y z F $.  $d u y z J $.  $d u K $.  $d u v w x y M $.
    $d u v w x y z N $.  $d u y z ph $.  $d v w X $.  $d v w x y Y $.
    ismtyhmeo.1 $e |- J = ( MetOpen ` M ) $.
    ismtyhmeo.2 $e |- K = ( MetOpen ` N ) $.
    ${
      ismtyhmeolem.3 $e |- X = dom dom M $.
      ismtyhmeolem.4 $e |- Y = dom dom N $.
      ismtyhmeolem.5 $e |- ( ph -> ( M e. Met /\ N e. Met ) ) $.
      ismtyhmeolem.6 $e |- ( ph -> F e. ( M Ismty N ) ) $.
      $( Lemma for ~ ismtyhmeo .  (The proof was shortened by Mario Carneiro,
         16-Jan-2014.) $)
      ismtyhmeolem $p |- ( ph -> A. u e. J ( F " u ) e. K ) $=
        ( vy wcel wss wa syl vx vz vw vv cv cima cbl cfv wrex wral wceq wfun wi
        crn wf wf1o co cismty cme isismty mpbid simpld f1of ffun adantr fvelima
        wb ex w3a crp opni2 syl3an1 simpl1 simprd simpl2 elssuni uniopn2 sseq2d
        cuni biimpar sylan2 syl2anc simpl3 sseldd ffvelrn simprl blelrn syl3anc
        blcntr ssel2 sylan 3impa ismtyima syl32anc ad2antll eleq2 sseq1 anbi12d
        imass2 eqsstr3d rcla4ev syl12anc expr rexlimdva mpd 3expa eleq1 rexbidv
        anbi1d syl5ibcom syld ralrimiv wfo f1ofo forn imassrn sseq2 3syl jctild
        mpbii isopn sylibrd ) ACBUEZUFZEQZBDAYCDQZYDIRZPUEZUAUEZQZYIYDRZSZUAGUG
        UHZUNZUIZPYDUJZSZYEAYFYPYGAYFYPAYFSZYOPYDYRYHYDQZUBUEZCUHZYHUKZUBYCUIZY
        OYRCULZYSUUCUMAUUDYFAHICUOZUUDAHICUPZUUEAUUFUCUEZUDUEZFUQUUGCUHUUHCUHGU
        QUKUDHUJUCHUJZACFGURUQQZUUFUUISZOAFUSQZGUSQZSUUJUUKVGNUCUDCFGHILMUTTVAV
        BZHICVCTZHICVDTVEUUDYSUUCUBYHYCCVFVHTYRUUBYOUBYCYRYTYCQZSUUAYIQZYKSZUAY
        NUIZUUBYOAYFUUPUUSAYFUUPVIZYTYHFUGUHUQZYCRZPVJUIZUUSAUULYFUUPUVCAUULUUM
        NVBZPYCFYTDJVKVLUUTUVBUUSPVJUUTYHVJQZUVBUUSUUTUVEUVBSZSZUUAYHYMUQZYNQZU
        UAUVHQZUVHYDRZUUSUVGUUMUUAIQZUVEUVIUVGAUUMAYFUUPUVFVMZAUULUUMNVNZTZUVGU
        UEYTHQZUVLUVGAUUEUVMUUOTUVGYCHYTUVGAYFYCHRZUVMAYFUUPUVFVOYFAYCDVSZRZUVQ
        YCDVPAUVQUVSAHUVRYCAUULHUVRUKUVDFDHLJVQTVRVTWAZWBAYFUUPUVFWCWDHIYTCWEWB
        ZUUTUVEUVBWFZGUUAYHIMWGWHUVGUUMUVLUVEUVJUVOUWAUWBGUUAYHIMWIWHUVGUVHCUVA
        UFZYDUVGUULUUMUUJUVPUVEUWCUVHUKUVGAUULUVMUVDTUVOUVGAUUJUVMOTUUTUVPUVFAY
        FUUPUVPYRUVQUUPUVPUVTYCHYTWJWKWLVEUWBYTYHCFGHLWMWNUVBUWCYDRUUTUVEUVAYCC
        WSWOWTUURUVJUVKSUAUVHYNYIUVHUKUUQUVJYKUVKYIUVHUUAWPYIUVHYDWQWRXAXBXCXDX
        EXFUUBUURYLUAYNUUBUUQYJYKUUAYHYIXGXIXHXJXDXKXLVHAUUFYGUUNUUFHICXMCUNZIU
        KZYGHICXNHICXOUWEYDUWDRYGCYCXPUWDIYDXQXTXRTXSAUUMYEYQVGUVNPUAYDGEIMKYAT
        YBXL $.
        $( [16-Jan-2014] $) $( [2-Sep-2009] $)
    $}

    $( An isometry is a homeomorphism on the induced topology.  (Contributed by
       Jeff Madsen, 2-Sep-2009.)  (The proof was shortened by Mario Carneiro,
       19-Jan-2014.) $)
    ismtyhmeo $p |- ( ( M e. Met /\ N e. Met ) ->
                            ( F e. ( M Ismty N ) -> F e. ( J Homeo K ) ) ) $=
      ( vu vx vy cme wcel wa cismty co cv wral eqid cdm wceq chomeosm cuni wf1o
      cima ccnv w3a ctop opntop ishomeo syl2an adantr isismty simprbda ad2antrr
      wb uniopn2 ad2antlr f1oeq23 syl2anc mpbid simpl simpr ismtyhmeolem ancomd
      cfv ismtycnv imp mpbir3and ex ) DKLZEKLZMZADENOLZABCUAOLZVLVMMZVNBUBZCUBZ
      AUCZAHPZUDCLHBQZAUEZVSUDBLHCQZVLVNVRVTWBUFUOZVMVJBUGLCUGLWCVKDBFUHECGUHHA
      BCVPVQVPRVQRUIUJUKVODSSZESSZAUCZVRVLVMWFIPZJPZDOWGAVEWHAVEEOTJWDQIWDQIJAD
      EWDWEWDRZWERZULUMVOWDVPTZWEVQTZWFVRUOVJWKVKVMDBWDWIFUPUNVKWLVJVMECWEWJGUP
      UQWDVPWEVQAURUSUTVOHABCDEWDWEFGWIWJVLVMVAZVLVMVBVCVOHWACBEDWEWDGFWJWIVOVJ
      VKWMVDVLVMWAEDNOLADEVFVGVCVHVI $.
      $( [19-Jan-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d r w x y z F $.  $d r w x y z M $.  $d r w y z N $.
    $( Lemma for ~ ismtybnd .  (The proof was shortened by Mario Carneiro,
       19-Jan-2014.) $)
    ismtybndlem $p |- ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) )
                                              -> ( M e. Bnd -> N e. Bnd ) ) $=
      ( vx vr vy vz vw cme wcel co cdm cv cfv wceq crp wrex wral wa wi w3a cbnd
      cismty cbl ccnv wf1o eqid isismty biimp3a simpld f1ocnv f1of 3syl ffvelrn
      sylan opreq1 eqeq2d rexbidv rcla4v syl cima imaeq2 wfo f1ofo foima adantr
      wf ex anim1d ismtyima syldan simpl f1ocnvfv2 syl2an opreq1d eqtrd eqeq12d
      imp syl5ib anassrs reximdva ralrimdva wb isbnd2 3ad2ant1 3ad2ant2 3imtr4d
      syld ) BIJZCIJZABCUCKJZUAZBLLZDMZEMZBUDNZKZOZEPQZDWMRZCLLZFMZWOCUDNZKZOZE
      PQZFXARZBUBJZCUBJZWLWTXFFXAWLXBXAJZSZWTWMXBAUEZNZWOWPKZOZEPQZXFXKXMWMJZWT
      XPTWLXAWMXLVGZXJXQWLWMXAAUFZXAWMXLUFXRWLXSGMZHMZBKXTANYAANCKOHWMRGWMRZWIW
      JWKXSYBSGHABCWMXAWMUGZXAUGZUHUIUJZWMXAAUKXAWMXLULUMZXAWMXBXLUNZUOWSXPDXMW
      MWNXMOZWRXOEPYHWQXNWMWNXMWOWPUPUQURUSUTXKXOXEEPWLXJWOPJZXOXETXOAWMVAZAXNV
      AZOWLXJYISZSZXEWMXNAVBYMYJXAYKXDWLYJXAOZYLWLXSWMXAAVCYNYEWMXAAVDWMXAAVEUM
      VFYMYKXMANZWOXCKZXDWLYLXQYISZYKYPOWLYLYQWLXJXQYIWLXRXJXQTYFXRXJXQYGVHUTVI
      VRXMWOABCWMYCVJVKYMYOXBWOXCWLXSXJYOXBOYLYEXJYIVLWMXAXBAVMVNVOVPVQVSVTWAWH
      WBWIWJXHWTWCWKDBWMEYCWDWEWJWIXIXGWCWKFCXAEYDWDWFWG $.
      $( [19-Jan-2014] $) $( [2-Sep-2009] $)

    $( Isometries preserve boundedness.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    ismtybnd $p |- ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) )
                                              -> ( M e. Bnd <-> N e. Bnd ) ) $=
      ( cme wcel cismty co w3a cbnd ismtybndlem ismtycnv 3impia 3com12 syld3an3
      ccnv wi impbid ) BDEZCDEZABCFGEZHBIEZCIEZABCJRSTAOZCBFGEZUBUAPZRSTUDABCKL
      SRUDUEUCCBJMNQ $.
      $( [19-Jan-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d X u v x y $.  $d M u v x y $.  $d N u v x y $.  $d A u v x y $.
    $d B u v x y $.  $d S u v x y $.  $d T u v x y $.  $d F u v x y $.
    ismtyres.1 $e |- X = dom dom M $.
    ismtyres.2 $e |- B = ( F " A ) $.
    ismtyres.3 $e |- S = ( M |` ( A X. A ) ) $.
    ismtyres.4 $e |- T = ( N |` ( B X. B ) ) $.
    $( A restriction of an isometry is an isometry.  The condition ` A C_ X `
       is not necessary but makes the proof easier.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    ismtyres $p |- ( ( ( M e. Met /\ N e. Met ) /\ ( F e. ( M Ismty N )
                              /\ A C_ X ) ) -> ( F |` A ) e. ( S Ismty T ) ) $=
      ( cme wcel wa co cdm cfv wceq wb vx vy vu vv wss cismty cres wf1o cv wral
      wf1 f1of1 f1ores ancoms adantll cxp metssba2 dmeqi syl6eqr f1oeq2 adantlr
      cima syl adantr wf f1f crn imassrn frn syl5ss adantl eqid anassrs xpeq12i
      sylan2 reseq2i eqtri f1oeq3 adantlll bitrd ex syl5 adantrd eleq2d anbi12d
      mpbid wi ssel anim12d opreq1 fveq2 opreq1d eqeq12d opreq2 opreq2d rcla42v
      imp an32s adantlrl opreqi oprvres syl5eq fvres ad2antrl ad2antll opreq12d
      wfun f1ofun f1odm sseq2d biimparc funfvima2 syl2anc adantrr adantrl eqtrd
      syl6eleqr adantlrr 3eqtr4d sylbird ralrimivv jcad isismty metres syl5eqel
      syl2an 3imtr4d impr ancom2s ) FMNZGMNZOZAHUEZEFGUFPNZEAUGZCDUFPNZYLYMYNYP
      YLYMOZHGQQZEUHZUAUIZUBUIZFPZYTERZUUAERZGPZSZUBHUJUAHUJZOZCQZQZDQZQZYOUHZU
      CUIZUDUIZCPZUUNYORZUUOYORZDPZSZUDUUJUJUCUUJUJZOZYNYPYQUUHUUMUVAYQYSUUMUUG
      YSHYREUKZYQUUMHYREULYQUVCUUMYQUVCOZAEAVBZYOUHZUUMYMUVCUVFYLUVCYMUVFHYRAEU
      MUNUOUVDUVFUUJUVEYOUHZUUMYQUVFUVGTZUVCYJYMUVHYKYJYMOZAUUJSUVHUVIAFAAUPUGZ
      QZQUUJFHAIUQUUIUVKCUVJKURURUSZAUUJUVEYOUTVCVAVDYKYMUVCUVGUUMTZYJYKYMOUVCO
      ZUVEUULSUVMUVNUVEGUVEUVEUPZUGZQZQZUULYKYMUVCUVEUVRSZYMUVCOYKUVEYRUEZUVSUV
      CYMHYREVEZUVTHYREVFUWAUVTYMUWAUVEEVGYREAVHHYREVIVJVKVOGYRUVEYRVLZUQVOVMUU
      KUVQDUVPDGBBUPZUGZUVPLUWCUVOGBUVEBUVEJJVNVPVQURURUSUVEUULUUJYOVRVCVSVTWFW
      AWBWCYQUUHUVAYQUUHOZUUTUCUDUUJUUJUWEUUNUUJNZUUOUUJNZOZUUNANZUUOANZOZUUTYQ
      UWKUWHTZUUHYJYMUWLYKUVIUWIUWFUWJUWGUVIAUUJUUNUVLWDUVIAUUJUUOUVLWDWEVAVDUW
      EUWKUUTUWEUWKOUUNUUOFPZUUNERZUUOERZGPZUUPUUSYMUUHUWKUWMUWPSZYLYMUUGUWKUWQ
      YSYMUWKUUGUWQYMUWKOZUUGUWQUWRUUNHNZUUOHNZOZUUGUWQWGYMUWKUXAYMUWIUWSUWJUWT
      AHUUNWHAHUUOWHWIWQUUFUWQUUNUUAFPZUWNUUDGPZSUAUBUUNUUOHHYTUUNSZUUBUXBUUEUX
      CYTUUNUUAFWJUXDUUCUWNUUDGYTUUNEWKWLWMUUAUUOSZUXBUWMUXCUWPUUAUUOUUNFWNUXEU
      UDUWOUWNGUUAUUOEWKWOWMWPVCWQWRWSVSUWKUUPUWMSUWEUWKUUPUUNUUOUVJPUWMCUVJUUN
      UUOKWTUUNUUOAAFXAXBVKYMUUHUWKUUSUWPSZYLYMYSUWKUXFUUGYMYSOZUWKOZUUSUWNUWOD
      PZUWPUXHUUQUWNUURUWODUWIUUQUWNSUXGUWJUUNAEXCXDUWJUURUWOSUXGUWIUUOAEXCXEXF
      UXHUXIUWNUWOUWDPZUWPDUWDUWNUWOLWTUXHUWNBNZUWOBNZUXJUWPSUXGUWIUXKUWJUXGUWI
      OUWNUVEBUXGUWIUWNUVENZUXGEXGZAEQZUEZUWIUXMWGYSUXNYMHYREXHVKZYSUXPYMYSUXOH
      AHYREXIXJXKZAUUNEXLXMWQJXQXNUXGUWJUXLUWIUXGUWJOUWOUVEBUXGUWJUWOUVENZUXGUX
      NUXPUWJUXSWGUXQUXRAUUOEXLXMWQJXQXOUWNUWOBBGXAXMXBXPXRVSXSWAXTYAWAYBYLYNUU
      HTYMUAUBEFGHYRIUWBYCVDYLYPUVBTZYMYJCMNDMNUXTYKYJCUVJMKFAYDYEYKDUWDMLGBYDY
      EUCUDYOCDUUJUULUUJVLUULVLYCYFVDYGYHYI $.
      $( [5-Jan-2014] $) $( [2-Sep-2009] $)
  $}

