$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Isometries
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Ismty $.
  $( Extend class notation with the class of metric space isometries. $)
  cismty $a class Ismty $.

  ${
    $d m n z f x y $.
    $( Define a function which takes two metric spaces and returns the set of
       isometries between the spaces.  An isometry is a bijection which
       preserves distance. $)
    df-ismty $a |- Ismty = { <. <. m , n >. , z >. | ( ( m e. Met /\ n e. Met )
        /\ z = { f | ( f : dom dom m -1-1-onto-> dom dom n /\ A. x e. dom dom m
            A. y e. dom dom m ( x m y ) = ( ( f ` x ) n ( f ` y ) ) ) } ) } $.
  $}

  ${
    $d M m n z f x y $.  $d N m n z f x y $.  $d X m n z f x y $.
    $d Y m n z f x y $.
    ismtyval.1 $e |- X = dom dom M $.
    ismtyval.2 $e |- Y = dom dom N $.
    $( The set of isometries between two metric spaces. $)
    ismtyval $p |- ( ( M e. Met /\ N e. Met ) -> ( M Ismty N ) =
                      { f | ( f : X -1-1-onto-> Y /\ A. x e. X A. y e. X
                            ( x M y ) = ( ( f ` x ) N ( f ` y ) ) ) } ) $=
      ( vm vn vz cme wcel cv wf1o co cfv wceq wral wa cab cvv cismty wf wss
      f1of adantr ss2abi cdm dmexg syl syl5eqel mapex syl2an ssexg sylancr wb
      dmeq dmeqd syl6eqr f1oeq2 opreq eqeq1d raleqbidv anbi12d abbidv f1oeq3
      eqeq2d 2ralbidv df-ismty oprabval2g mpd3an3 ) DMNZEMNZFGCOZPZAOZBOZDQZVRV
      PRZVSVPRZEQZSZBFTAFTZUAZCUBZUCNZDEUDQWGSVNVOUAWGFGVPUEZCUBZUFWJUCNZWHWFWI
      CVQWIWEFGVPUGUHUIVNFUCNGUCNWKVOVNFDUJZUJZUCHVNWLUCNWMUCNDMUKWLUCUKULUMVOG
      EUJZUJZUCIVOWNUCNWOUCNEMUKWNUCUKULUMFGUCUCCUNUOWGWJUCUPUQJKLDEMMJOZUJZUJZ
      KOZUJZUJZVPPZVRVSWPQZWAWBWSQZSZBWRTZAWRTZUAZCUBWGUDFXAVPPZVTXDSZBFTZAFTZU
      AZCUBUCWPDSZXHXMCXNXBXIXGXLXNWRFSXBXIURXNWRWMFXNWQWLWPDUSUTHVAZWRFXAVPVBU
      LXNXFXKAWRFXOXNXEXJBWRFXOXNXCVTXDVRVSWPDVCVDVEVEVFVGWSESZXMWFCXPXIVQXLWEX
      PXAGSXIVQURXPXAWOGXPWTWNWSEUSUTIVAXAGFVPVHULXPXJWDABFFXPXDWCVTWAWBWSEVCVI
      VJVFVGABLCJKVKVLVM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M f x y $.  $d N f x y $.  $d X f x y $.  $d Y f x y $.  $d F f x y $.
    isismty.1 $e |- X = dom dom M $.
    isismty.2 $e |- Y = dom dom N $.
    $( The condition "is an isometry". $)
    isismty $p |- ( ( M e. Met /\ N e. Met ) -> ( F e. ( M Ismty N ) <->
                          ( F : X -1-1-onto-> Y /\ A. x e. X A. y e. X
                              ( x M y ) = ( ( F ` x ) N ( F ` y ) ) ) ) ) $=
      ( vf cme wcel wa cismty co cv wf1o cfv wceq wral cab ismtyval eleq2d cvv
      wi wb wf f1of cdm dmexg syl syl5eqel fex syl2an expcom adantr adantrd
      f1oeq1 fveq1 opreq12d eqeq2d 2ralbidv anbi12d elab3g bitrd ) DKLZEKLZMZCD
      ENOZLCFGJPZQZAPZBPZDOZVLVJRZVMVJRZEOZSZBFTAFTZMZJUAZLZFGCQZVNVLCRZVMCRZEO
      ZSZBFTAFTZMZVHVIWACABJDEFGHIUBUCVHWICUDLZUEWBWIUFVHWCWJWHVFWCWJUEVGWCVFWJ
      WCFGCUGFUDLWJVFFGCUHVFFDUIZUIZUDHVFWKUDLWLUDLDKUJWKUDUJUKULFGUDCUMUNUOUPU
      QVTWIJCUDVJCSZVKWCVSWHFGVJCURWMVRWGABFFWMVQWFVNWMVOWDVPWEEVLVJCUSVMVJCUSU
      TVAVBVCVDUKVE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M u v x y $.  $d N u v x y $.  $d F u v x y $.
    $( The inverse of an isometry is an isometry. $)
    ismtycnv $p |- ( ( M e. Met /\ N e. Met ) ->
                      ( F e. ( M Ismty N ) -> `' F e. ( N Ismty M ) ) ) $=
      ( vx vy vu vv cme wcel wa cdm wf1o cv co cfv wceq wral ccnv cismty wi
      f1ocnv adantr f1ocnvdm ex anim12d imdistani opreq1 fveq2 opreq1d eqeq12d
      opreq2 opreq2d rcla42v impcom adantll syl f1ocnvfv2 adantrr adantrl
      opreq12d adantlr eqtr2d r19.21aivv jca a1i eqid isismty wb ancoms
      3imtr4d ) BHIZCHIZJZBKKZCKKZALZDMZEMZBNZVQAOZVRAOZCNZPZEVNQDVNQZJZVOVNARZ
      LZFMZGMZCNZWHWFOZWIWFOZBNZPZGVOQFVOQZJZABCSNIWFCBSNIZWEWPTVMWEWGWOVPWGWDV
      NVOAUAUBWEWNFGVOVOWEWHVOIZWIVOIZJZWNWEWTJZWMWKAOZWLAOZCNZWJXAWEWKVNIZWLVN
      IZJZJWMXDPZWEWTXGVPWTXGTWDVPWRXEWSXFVPWRXEVNVOWHAUCUDVPWSXFVNVOWIAUCUDUEU
      BUFWDXGXHVPXGWDXHWCXHWKVRBNZXBWACNZPDEWKWLVNVNVQWKPZVSXIWBXJVQWKVRBUGXKVT
      XBWACVQWKAUHUIUJVRWLPZXIWMXJXDVRWLWKBUKXLWAXCXBCVRWLAUHULUJUMUNUOUPVPWTXD
      WJPWDVPWTJXBWHXCWICVPWRXBWHPWSVNVOWHAUQURVPWSXCWIPWRVNVOWIAUQUSUTVAVBUDVC
      VDVEDEABCVNVOVNVFZVOVFZVGVLVKWQWPVHFGWFCBVOVNXNXMVGVIVJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M q r s t u v w x y z $.  $d N q r s t u v w x y z $.
    $d J q r s t u v w x y z $.  $d K q r s t u v w x y z $.
    $d F q r s t u v w x y z $.
    ismtyhmeo.1 $e |- J = ( MetOpen ` M ) $.
    ismtyhmeo.2 $e |- K = ( MetOpen ` N ) $.
    $( Lemma for ~ ismtyhmeo . $)
    ismtyhmeolem $p |- ( ( ( M e. Met /\ N e. Met ) /\
                ( F : dom dom M -1-1-onto-> dom dom N /\ A. x e. dom dom M
                    A. y e. dom dom M ( x M y ) = ( ( F ` x ) N ( F ` y ) ) ) )
                                              -> A. u e. J ( F " u ) e. K ) $=
      ( vv vs vw vt vz vq vr cme wcel wa cdm wf1o cv co cfv wceq wral cima wss
      cbl crn wrex wi wfun f1ofun fvelima ex syl adantr ad2antlr elequ1 anbi1d
      rexbidv rcla4cva cc0 clt wbr cr wf f1of ffvelrn sylan ad2ant2r ad2ant2lr
      simprlr simprrl simpll simprll simprr eqid blssm syl22anc adantlrr
      adantllr adantrrr sseld imp adantrr eleq1 syl5ibcom expimpd ancomsd
      ad2antrl ad2antrr expdimp anasss mpd opreq1 fveq2 opreq1d eqeq12d opreq2
      opreq2d rcla42v com12 ad2antll mp2and wb simpl elbl simplbda an32s
      eqbrtrrd breq1d impr jca expr r19.23adva ccnv simplr simprl f1ocnvdm
      adantll adantlr f1ocnvfv2 eqtrd biimpar mpbir2and eqeq1d rcla4ev
      syl11anc impbid imaeq2 adantl eleq2d wfn f1ofn fvelimab bitrd adantlll
      3bitr4d eqrdv breq2 eqeq2d anbi12d syl12anc anbi2d exp32 r19.23advv
      blrn3 3imtr4d sstr ancoms fdm sseq2d funfvima2 sylan2 anassrs ancom2s
      imass2 eleq2 sseq1 exp31 syld r19.21aiv wfo f1ofo forn imassrn sseq2
      mpbii 3syl jctild isopn ) GRSZHRSZTZGUAUAZHUAUAZDUBZAUCZBUCZGUDZUWADUEZUW
      BDUEZHUDZUFZBUVRUGAUVRUGZTZTZDCUCZUHZFSZCEUWJUWKUVRUIZKUCZLUCZSZUWPUWKUIZ
      TZLGUJUEZUKZULZKUWKUGZTZUWLUVSUIZMUCZNUCZSZUXGUWLUIZTZNHUJUEZUKZULZMUWLUG
      ZTZUWKESZUWMUWJUXDUXNUXEUWJUXDUXNUWJUXDTZUXMMUWLUXQUXFUWLSZOUCZDUEZUXFUFZ
      OUWKULZUXMUWIUXRUYBUMZUVQUXDUVTUYCUWHUVTDUNZUYCUVRUVSDUOZUYDUXRUYBOUXFUWK
      DUPUQURUSUTUWJUWNUXCUYBUXMUMUWJUWNTZUXCTUYAUXMOUWKUYFUXCUXSUWKSZUYAUXMUMZ
      UXCUYGTUYFUXSUWPSZUWRTZLUXAULZUYHUXBUYKKUXSUWKUWOUXSUFZUWSUYJLUXAUYLUWQUY
      IUWRKOLVAVBVCVDUYFUYKUYHUYFUYJUYHLUXAUYFUWPUXASZTZUYJUYAUXMUYNUYJTZUYATDU
      WPUHZUXLSZUXFUYPSZUYPUWLUIZUXMUYNUYQUYJUYAUWJUYMUYQUWNUWJUYMUYQUWJVEPUCZV
      FVGZUWPUWOUYTUWTUDZUFZTZPVHULKUVRULZVEQUCZVFVGZUYPUXFVUFUXKUDZUFZTZQVHULZ
      MUVSULZUYMUYQUWJVUDVULKPUVRVHUWJUWOUVRSZUYTVHSZTZVUDVULUWJVUOVUDTZTZUWODU
      EZUVSSZVUGUYPVURVUFUXKUDZUFZTZQVHULZVULUWIVUOVUSUVQVUDUVTVUMVUSUWHVUNUVTU
      VRUVSDVIZVUMVUSUVRUVSDVJZUVRUVSUWODVKVLZVMVNVUQVUNVUAUYPVURUYTUXKUDZUFZVV
      CUWJVUMVUNVUDVOUWJVUOVUAVUCVPVUQOUYPVVGVUQUXFDUEZUXSUFZMVUBULZUXSUVSSZVUR
      UXSHUDZUYTVFVGZTZUXSUYPSZUXSVVGSZVUQVVKVVOVUQVVJVVOMVUBVUQUXFVUBSZVVJVVOV
      UQVVRVVJTTZVVLVVNVVSUXFUVRSZVVLVUQVVRVVTVVJVUQVVRVVTVUQVUBUVRUXFUWJVUOVUA
      VUBUVRUIZVUCUVOUWIVUOVUATZVWAUVPUVOUVTVWBVWAUWHUVOUVTTZVWBTZUVOVUMVUNVUAV
      WAUVOUVTVWBVQVWCVUMVUNVUAVRVWCVUMVUNVUAVOVWCVUOVUAVSGUWOUYTUVRUVRVTZWAWBZ
      WCWDWEWFWGZWHVUQVVRVVJVVTVVLUMVUQVVRTZVVJVVTVVLUWJVVJVVTTVVLUMZVUPVVRUVTV
      WIUVQUWHUVTVVTVVJVVLUVTVVTVVJVVLUVTVVTTVVIUVSSZVVJVVLUVTVVDVVTVWJVVEUVRUV
      SUXFDVKVLVVIUXSUVSWIWJWKWLWMWNWOWPWQVUQVVRVVJVVNVWHVURVVIHUDZUYTVFVGVVJVV
      NVWHUWOUXFGUDZVWKUYTVFVWHVUMVVTVWLVWKUFZVUPVUMUWJVVRVUMVUNVUDVQZUTVWGUWJV
      UMVVTTZVWMUMZVUPVVRUWHVWPUVQUVTVWOUWHVWMUWGVWMUWOUWBGUDZVURUWEHUDZUFZABUW
      OUXFUVRUVRUWAUWOUFZUWCVWQUWFVWRUWAUWOUWBGWRVWTUWDVURUWEHUWAUWODWSWTXAZUWB
      UXFUFZVWQVWLVWRVWKUWBUXFUWOGXBVXBUWEVVIVURHUWBUXFDWSXCXAXDXEXFWNXGUVQVUPV
      VRVWLUYTVFVGZUWIUVOVUPVVRVXCUVPUVOVVRVUPVXCUVOVVRTVUOVUAVXCVUCUVOVWBVVRVX
      CUVOVWBTZVVRVVTVXCVXDUVOVUMVUNVUAVVRVVTVXCTXHUVOVWBXIUVOVUMVUNVUAVRUVOVUM
      VUNVUAVOUVOVUOVUAVSUXFGUWOUYTUVRVWEXJWBXKXLWEXLWDWDXMVVJVWKVVMUYTVFVVIUXS
      VURHXBXNWJXOXPXQXRVUQVVOVVKVUQVVOTZUXSDXSUEZVUBSZVXFDUEZUXSUFZVVKVXEVXGVX
      FUVRSZUWOVXFGUDZUYTVFVGZVXEUVOVUMVUNVUAVXGVXJVXLTXHUWJUVOVUPVVOUVOUVPUWIV
      QWNVUPVUMUWJVVOVWNUTVUPVUNUWJVVOVUMVUNVUDXTUTVUPVUAUWJVVOVUOVUAVUCYAUTVXF
      GUWOUYTUVRVWEXJWBUWJVVLVXJVUPVVNUVQUVTVVLVXJUWHUVTVVLVXJUVQUVRUVSUXSDYBYC
      WCZVMVUQVVLVVNVXLVUQVVLTZVXLVVNVXNVXKVVMUYTVFVXNVXKVURVXHHUDZVVMVXNVUMVXJ
      VXKVXOUFZVUPVUMUWJVVLVWNUTUWJVVLVXJVUPVXMYDUWJVUMVXJTZVXPUMZVUPVVLUWHVXRU
      VQUVTVXQUWHVXPUWGVXPVWSABUWOVXFUVRUVRVXAUWBVXFUFZVWQVXKVWRVXOUWBVXFUWOGXB
      VXSUWEVXHVURHUWBVXFDWSXCXAXDXEXFWNXGVXNVXHUXSVURHUWJVVLVXIVUPUVQUVTVVLVXI
      UWHUVTVVLVXIUVQUVRUVSUXSDYEYCWCZYDXCYFXNYGWPYHUWJVVLVXIVUPVVNVXTVMVVJVXIM
      VXFVUBUXFVXFUFVVIVXHUXSUXFVXFDWSYIYJYKUQYLVUQVVPUXSDVUBUHZSZVVKVUQUYPVYAU
      XSVUDUYPVYAUFZUWJVUOVUCVYCVUAUWPVUBDYMYNXFYOUWJVUOVUAVYBVVKXHZVUCUVQUVTVW
      BVYDUWHUVOUVTVWBVYDUVPVWDDUVRYPZVWAVYDUVTVYEUVOVWBUVRUVSDYQUTVWFMUVRVUBUX
      SDYRYKWDWCWEYSUWJVUOVUAVVQVVOXHZVUCUVQUVTVWBVYFUWHUVPUVTVWBVYFUVOUVPUVTTZ
      VWBTUVPVUSVUNVUAVYFUVPUVTVWBVQUVTVUOVUSUVPVUAUVTVUMVUSVUNVVFWHVNVYGVUMVUN
      VUAVOVYGVUOVUAVSUXSHVURUYTUVSUVSVTZXJWBYTWCWEUUAUUBVVBVUAVVHTQUYTVHVUFUYT
      UFZVUGVUAVVAVVHVUFUYTVEVFUUCVYIVUTVVGUYPVUFUYTVURUXKXBUUDUUEYJUUFVUKVVCMV
      URUVSUXFVURUFZVUJVVBQVHVYJVUIVVAVUGVYJVUHVUTUYPUXFVURVUFUXKWRUUDUUGVCYJYK
      UUHUUIUVOUYMVUEXHUVPUWIKPUWPGUVRVWEUUJWNUVPUYQVULXHUVOUWIMQUYPHUVSVYHUUJU
      TUUKWGYDWNUYOUYAUYRUYOUXTUYPSZUYAUYRUYNUWRUYIVYKUYNUWRUYIVYKUYFUWRUYIVYKU
      MZUYMUWJUWNUWRVYLUWNUWRTUWJUWPUVRUIZVYLUWRUWNVYMUWPUWKUVRUULUUMUWIVYMVYLU
      VQUVTVYMVYLUWHUVTVYMTUYDUWPDUAZUIZVYLUVTUYDVYMUYEUSUVTVYOVYMUVTVYNUVRUWPU
      VTVVDVYNUVRUFVVEUVRUVSDUUNURUUOYGUWPUXSDUUPYKYDYCUUQUURYDXOUUSUXTUXFUYPWI
      WJWGUYJUYSUYNUYAUWRUYSUYIUWPUWKDUUTYNUTUXJUYRUYSTNUYPUXLUXGUYPUFUXHUYRUXI
      UYSUXGUYPUXFUVAUXGUYPUWLUVBUUEYJUUFUVCXRWGUUQUURXRWPUVDUVEUQUVTUXEUVQUWHU
      VTUVRUVSDUVFDUKZUVSUFZUXEUVRUVSDUVGUVRUVSDUVHVYQUWLVYPUIUXEDUWKUVIVYPUVSU
      WLUVJUVKUVLWMUVMUVOUXPUXDXHUVPUWIKLUWKGEUVRVWEIUVNWNUVPUWMUXOXHUVOUWIMNUW
      LHFUVSVYHJUVNUTUUKUVE $.
      $( [2-Sep-2009] $)

    $( An isometry is a homeomorphism on the induced topology. $)
    ismtyhmeo $p |- ( ( M e. Met /\ N e. Met ) ->
                            ( F e. ( M Ismty N ) -> F e. ( J Homeo K ) ) ) $=
      ( vx vy vu vs vt cme wcel wa cdm wf1o cv co cfv wceq wral cuni cima ccnv
      w3a cismty chomeosm wb eqid uniopn2 eqcomd adantr f1oeq2 syl adantl
      f1oeq3 bitrd biimpd adantrd ismtyhmeolem ex f1ocnv wi f1ocnvdm anim12d
      imdistani opreq1 fveq2 opreq1d eqeq12d opreq2 opreq2d rcla42v impcom
      adantll f1ocnvfv2 adantrr adantrl opreq12d adantlr eqtr2d r19.21aivv jca
      sylan2 ancom1s 3jcad isismty ctop opntop ishomeo2 syl2an 3imtr4d ) DMNZEM
      NZOZDPPZEPPZAQZHRZIRZDSZWTATZXAATZESZUAZIWQUBHWQUBZOZBUCZCUCZAQZAJRZUDCNJ
      BUBZAUEZXLUDBNJCUBZUFZADEUGSNABCUHSNZWPXHXKXMXOWPWSXKXGWPWSXKWPWSXIWRAQZX
      KWPWQXIUAZWSXRUIWNXSWOWNXIWQDBWQWQUJZFUKULUMWQXIWRAUNUOWPWRXJUAZXRXKUIWOY
      AWNWOXJWRECWRWRUJZGUKULUPWRXJXIAUQUOURUSUTWPXHXMHIJABCDEFGVAVBWPXHXOWOWNX
      HXOXHWOWNOWRWQXNQZKRZLRZESZYDXNTZYEXNTZDSZUAZLWRUBKWRUBZOXOXHYCYKWSYCXGWQ
      WRAVCUMXHYJKLWRWRXHYDWRNZYEWRNZOZYJXHYNOZYIYGATZYHATZESZYFYOXHYGWQNZYHWQN
      ZOZOYIYRUAZXHYNUUAWSYNUUAVDXGWSYLYSYMYTWSYLYSWQWRYDAVEVBWSYMYTWQWRYEAVEVB
      VFUMVGXGUUAUUBWSUUAXGUUBXFUUBYGXADSZYPXDESZUAHIYGYHWQWQWTYGUAZXBUUCXEUUDW
      TYGXADVHUUEXCYPXDEWTYGAVIVJVKXAYHUAZUUCYIUUDYRXAYHYGDVLUUFXDYQYPEXAYHAVIV
      MVKVNVOVPUOWSYNYRYFUAXGWSYNOYPYDYQYEEWSYLYPYDUAYMWQWRYDAVQVRWSYMYQYEUAYLW
      QWRYEAVQVSVTWAWBVBWCWDKLJXNCBEDGFVAWEWFVBWGHIADEWQWRXTYBWHWNBWINCWINXQXPU
      IWODBFWJECGWJJABCXIXJXIUJXJUJWKWLWM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M r u v x y z $.  $d N r u v x y z $.  $d F r u v x y z $.
    $( Lemma for ~ ismtybnd . $)
    ismtybndlem $p |- ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) )
                                              -> ( M e. Bnd -> N e. Bnd ) ) $=
      ( vx vr vy vu vv vz cme wcel cismty co w3a cdm cv cbl cfv wceq crp wrex
      wral cbnd wi wa wf1o eqid isismty ccnv f1ocnvdm adantlr adantll opreq1
      eqeq2d rexbidv rcla4cva clt wbr ex anim12d adantr imdistani fveq2
      opreq1d eqeq12d opreq2 opreq2d rcla42v impcom syl anassrs adantlrr
      f1ocnvfv2 ad2ant2r ad2antrr adantllr adantlll opreq12d eqtr2d eleq2
      syl5ibcom imp an32s cr cc0 wb simplll rpregt0 ad2antll elbl2 syl31anc
      mpbid eqbrtrd simpllr simprl simpr mpbird ssrdv wss elrp blssm sylan2b
      anasss eqssd reximdva syl5 expdimp mpd r19.21aiva exp31 sylbid 3impia
      isbnd2 3ad2ant1 3ad2ant2 3imtr4d ) BJKZCJKZABCLMKZNBOOZDPZEPZBQRZMZSZETUA
      ZDXTUBZCOOZFPZYBCQRMZSZETUAZFYHUBZBUCKZCUCKZXQXRXSYGYMUDZXQXRUEZXSXTYHAUF
      ZGPZHPZBMZYSARZYTARZCMZSZHXTUBGXTUBZUEZYPGHABCXTYHXTUGZYHUGZUHYQUUGYGYMYQ
      UUGUEZYGUEZYLFYHUUKYIYHKZUEYIAUIZRZXTKZYLUUJUULUUOYGUUGUULUUOYQYRUULUUOUU
      FXTYHYIAUJZUKULUKUUJUULYGUUOYLUDUUJUULUEZYGUUOYLYGUUOUEXTUUNYBYCMZSZETUAZ
      UUQYLYFUUTDUUNXTYAUUNSZYEUUSETUVAYDUURXTYAUUNYBYCUMUNUOUPUUQUUSYKETUUJUUL
      YBTKZUUSYKUDUUJUULUVBUEZUEZUUSYKUVDUUSUEZYHYJUVEIYHYJUVEIPZYHKZUVFYJKZUVE
      UVGUEZUVHYIUVFCMZYBUQURZUVIUVJUUNUVFUUMRZBMZYBUQUVIUVMUUNARZUVLARZCMZUVJU
      VDUVGUVMUVPSZUUSUUJUULUVGUVQUVBUUJUULUVGUVQUUGUULUVGUEZUVQYQUUGUVRUEUUGUU
      OUVLXTKZUEZUEUVQUUGUVRUVTYRUVRUVTUDUUFYRUULUUOUVGUVSYRUULUUOUUPUSYRUVGUVS
      XTYHUVFAUJZUSUTVAVBUUFUVTUVQYRUVTUUFUVQUUEUVQUUNYTBMZUVNUUCCMZSGHUUNUVLXT
      XTYSUUNSZUUAUWBUUDUWCYSUUNYTBUMUWDUUBUVNUUCCYSUUNAVCVDVEYTUVLSZUWBUVMUWCU
      VPYTUVLUUNBVFUWEUUCUVOUVNCYTUVLAVCVGVEVHVIULVJULVKVLUKUVIUVNYIUVOUVFCUVDU
      VNYISZUUSUVGUUGUVCUWFYQYRUULUWFUUFUVBXTYHYIAVMVNULVOUVDUVGUVOUVFSZUUSUUGU
      VCUVGUWGYQYRUVCUVGUWGUUFYRUVGUWGUVCXTYHUVFAVMUKVPVQUKVRVSUVIUVLUURKZUVMYB
      UQURZUVDUVGUUSUWHUVDUVGUEZUUSUWHUWJUVSUUSUWHUUJUVGUVSUVCUUGUVGUVSYQYRUVGU
      VSUUFUWAUKULUKZXTUURUVLVTWAWBWCUVIXQUUOUVSYBWDKWEYBUQURUEZUWHUWIWFUVDXQUU
      SUVGXQXRUUGUVCWGVOUVDUUOUUSUVGUUGUVCUUOYQYRUULUUOUUFUVBUUPVNULVOUVDUVGUVS
      UUSUWKUKUVDUWLUUSUVGUVBUWLUUJUULYBWHWIVOZUVLBUUNYBXTUUHWJWKWLWMUVIXRUULUV
      GUWLUVHUVKWFUVDXRUUSUVGXQXRUUGUVCWNVOUVDUULUUSUVGUUJUULUVBWOVOUVEUVGWPUWM
      UVFCYIYBYHUUIWJWKWQUSWRUVDYJYHWSZUUSYQUVCUWNUUGXRUVCUWNXQXRUULUVBUWNUVBXR
      UULUEUWLUWNYBWTCYIYBYHUUIXAXBXCULUKVAXDUSVKXEXFXGWCXHXIXJXKXLXQXRYNYGWFXS
      DBXTEUUHXMXNXRXQYOYMWFXSFCYHEUUIXMXOXP $.
      $( [2-Sep-2009] $)

    $( Isometries preserve boundedness. $)
    ismtybnd $p |- ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) )
                                              -> ( M e. Bnd <-> N e. Bnd ) ) $=
      ( cme wcel cismty co w3a cbnd ismtybndlem wi wa ccnv ismtycnv 3expia
      ancoms syld 3impia impbid ) BDEZCDEZABCFGEZHBIEZCIEZABCJTUAUBUDUCKZTUALUB
      AMZCBFGEZUEABCNUATUGUEKUATUGUEUFCBJOPQRS $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X u v x y $.  $d M u v x y $.  $d N u v x y $.  $d A u v x y $.
    $d B u v x y $.  $d S u v x y $.  $d T u v x y $.  $d F u v x y $.
    ismtyres.1 $e |- X = dom dom M $.
    ismtyres.2 $e |- B = ( F " A ) $.
    ismtyres.3 $e |- S = ( M |` ( A X. A ) ) $.
    ismtyres.4 $e |- T = ( N |` ( B X. B ) ) $.
    $( A restriction of an isometry is an isometry.  The condition ` A C_ X `
       is not necessary but makes the proof easier. $)
    ismtyres $p |- ( ( ( M e. Met /\ N e. Met ) /\ ( F e. ( M Ismty N )
                              /\ A C_ X ) ) -> ( F |` A ) e. ( S Ismty T ) ) $=
      ( vx vy vu vv cme wcel wa wss cismty co cres cdm wf1o cv cfv wceq wral
      wf1 f1of1 cima f1ores ancoms adantll wb basmetres f1oeq2 syl adantlr
      adantr cxp wf f1f crn imassrn a1i frn sstrd adantl sylan2 eqid metssba2
      anassrs xpeq12i reseq2i eqtri dmeqi syl6eqr f1oeq3 adantlll bitrd mpbid
      ex syl5 adantrd eleq2d anbi12d wi ssel anim12d imp opreq1 fveq2 opreq1d
      eqeq12d opreq2 opreq2d rcla42v an32s adantlrl opreqi oprvres syl5eq
      fvres ad2antrl ad2antll opreq12d wfun f1ofun f1of fdm sseq2d biimparc
      funfvima2 syl11anc syl6eleqr adantrr adantrl eqtrd adantlrr 3eqtr4d
      sylbird r19.21aivv jcad isismty metres syl5eqel syl2an 3imtr4d impr
      ancom2s ) FQRZGQRZSZAHTZEFGUAUBRZEAUCZCDUAUBRZYOYPYQYSYOYPSZHGUDUDZEUEZMU
      FZNUFZFUBZUUCEUGZUUDEUGZGUBZUHZNHUIMHUIZSZCUDUDZDUDZUDZYRUEZOUFZPUFZCUBZU
      UPYRUGZUUQYRUGZDUBZUHZPUULUIOUULUIZSZYQYSYTUUKUUOUVCYTUUBUUOUUJUUBHUUAEUJ
      ZYTUUOHUUAEUKYTUVEUUOYTUVESZAEAULZYRUEZUUOYPUVEUVHYOUVEYPUVHHUUAAEUMUNUOU
      VFUVHUULUVGYRUEZUUOYTUVHUVIUPZUVEYMYPUVJYNYMYPSZAUULUHUVJFCHAKIUQZAUULUVG
      YRURUSUTVAYNYPUVEUVIUUOUPZYMYNYPSUVESZUVGUUNUHUVMUVNUVGGUVGUVGVBZUCZUDZUD
      ZUUNYNYPUVEUVGUVRUHZYPUVESYNUVGUUATZUVSUVEYPHUUAEVCZUVTHUUAEVDUWAUVTYPUWA
      UVGEVEZUUAUVGUWBTUWAEAVFVGHUUAEVHVIVJVKGUUAUVGUUAVLZVMVKVNUUMUVQDUVPDGBBV
      BZUCZUVPLUWDUVOGBUVGBUVGJJVOVPVQVRVRVSUVGUUNUULYRVTUSWAWBWCWDWEWFYTUUKUVC
      YTUUKSZUVBOPUULUULUWFUUPUULRZUUQUULRZSZUUPARZUUQARZSZUVBYTUWLUWIUPZUUKYMY
      PUWMYNUVKUWJUWGUWKUWHUVKAUULUUPUVLWGUVKAUULUUQUVLWGWHUTVAUWFUWLUVBUWFUWLS
      UUPUUQFUBZUUPEUGZUUQEUGZGUBZUURUVAYPUUKUWLUWNUWQUHZYOYPUUJUWLUWRUUBYPUWLU
      UJUWRYPUWLSZUUJUWRUWSUUPHRZUUQHRZSZUUJUWRWIYPUWLUXBYPUWJUWTUWKUXAAHUUPWJA
      HUUQWJWKWLUUIUWRUUPUUDFUBZUWOUUGGUBZUHMNUUPUUQHHUUCUUPUHZUUEUXCUUHUXDUUCU
      UPUUDFWMUXEUUFUWOUUGGUUCUUPEWNWOWPUUDUUQUHZUXCUWNUXDUWQUUDUUQUUPFWQUXFUUG
      UWPUWOGUUDUUQEWNWRWPWSUSWLWTXAWAUWLUURUWNUHUWFUWLUURUUPUUQFAAVBUCZUBUWNCU
      XGUUPUUQKXBUUPUUQAAFXCXDVJYPUUKUWLUVAUWQUHZYOYPUUBUWLUXHUUJYPUUBSZUWLSZUV
      AUWOUWPDUBZUWQUXJUUSUWOUUTUWPDUWJUUSUWOUHUXIUWKUUPAEXEXFUWKUUTUWPUHUXIUWJ
      UUQAEXEXGXHUXJUXKUWOUWPUWEUBZUWQDUWEUWOUWPLXBUXJUWOBRZUWPBRZUXLUWQUHUXIUW
      JUXMUWKUXIUWJSUWOUVGBUXIUWJUWOUVGRZUXIEXIZAEUDZTZUWJUXOWIUUBUXPYPHUUAEXJV
      JZUUBUXRYPUUBUXQHAUUBUWAUXQHUHHUUAEXKHUUAEXLUSXMXNZAUUPEXOXPWLJXQXRUXIUWK
      UXNUWJUXIUWKSUWPUVGBUXIUWKUWPUVGRZUXIUXPUXRUWKUYAWIUXSUXTAUUQEXOXPWLJXQXS
      UWOUWPBBGXCXPXDXTYAWAYBWDYCYDWDYEYOYQUUKUPYPMNEFGHUUAIUWCYFVAYOYSUVDUPZYP
      YMCQRDQRUYBYNYMCUXGQKFAYGYHYNDUWEQLGBYGYHOPYRCDUULUUNUULVLUUNVLYFYIVAYJYK
      YL $.
      $( [2-Sep-2009] $)
  $}

