$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Product topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    txtopiOLD.1 $e |- R e. Top $.
    txtopiOLD.2 $e |- S e. Top $.
    $( The product of two topologies is a topology.  (Moved to ~ txtopi in main
       set.mm and may be deleted by mathbox owner, JM. --NM 15-Oct-2012.) $)
    txtopiOLD $p |- ( R tX S ) e. Top $=
      ( ctop wcel ctx co eqid txtop mp2an ) AEFBEFABGHZEFCDABLLIJK $.
      $( [15-Jun-2010] $)
  $}

  ${
    txuniiOLD.1 $e |- R e. Top $.
    txuniiOLD.2 $e |- S e. Top $.
    txuniiOLD.3 $e |- X = U. R $.
    txuniiOLD.4 $e |- Y = U. S $.
    $( The underlying set of the product of two topologies.  (Moved to ~ txunii
       in main set.mm and may be deleted by mathbox owner, JM. --NM
       15-Oct-2012.) $)
    txuniiOLD $p |- ( X X. Y ) = U. ( R tX S ) $=
      ( ctx co cuni cxp ctop wcel wceq eqid txuni mp2an eqcomi ) ABIJZKZCDLZAMN
      BMNUAUBOEFABTCDTPGHQRS $.
      $( [15-Jun-2010] $)
  $}

  ${
    $d T x y z w $.  $d R x y z w $.  $d S x y z w $.  $d U x y z w $.
    $d X x y z $.  $d Y x y z $.  $d F x y z w $.  $d G x y z w $.  $d H w $.
    $d Z x y z $.  $d W x y z w $.
    txcnoprab.1 $e |- T = ( R tX S ) $.
    txcnoprab.2 $e |- X = U. U $.
    txcnoprab.3 $e |- Y = U. W $.
    txcnoprab.4 $e |- Z = ( U tX W ) $.
    txcnoprab.5 $e |- H = { <. <. x , y >. , z >. | ( ( x e. X /\ y e. Y ) /\
                                      z = <. ( x F y ) , ( x G y ) >. ) } $.
    $( A map into the product of two topological spaces is continuous if both
       of its projections are continuous. $)
    txcnoprab $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( U e. Top /\ W e. Top ) /\
              ( F e. ( Z Cn R ) /\ G e. ( Z Cn S ) ) ) -> H e. ( Z Cn T ) ) $=
      ( vw ctop wcel wa ccn co wi c0 csn cif ctx cv cuni cop wceq copab2
      opreq1 opreq1d eleq2d anbi12d anbi2d unieq anbi1d oprabbidv eleq12d
      imbi12d opreq1i eleq2i anbi12i anbi2i anbi1i oprabbii eqtri eleq12i
      imbi12i syl5bb opreq2 sn0top elimel eqid txtop mp2an cxp txuni eqcomi
      cfv fveq2 opeq12d df-opr opeq12i syl5eq dfoprab4s txcnopab mp3anl3
      dedth2h imp an12s 3impb ) DUAUBZEUAUBZUCZGUAUBZKUAUBZUCZHNDUDUEZUBZINEUDU
      EZUBZUCZJNFUDUEZUBZXCWTXHXJXCWTXHUCZXJXAXBXKXJUFZWTHXAGUGUHZUIZKUJUEZDUDU
      EZUBZIXOEUDUEZUBZUCZUCZAUKZXNULZUBZBUKZKULZUBZUCZCUKYBYEHUEZYBYEIUEZUMZUN
      ZUCZABCUOZXOFUDUEZUBZUFZWTHXNXBKXMUIZUJUEZDUDUEZUBZIYSEUDUEZUBZUCZUCZYDYE
      YRULZUBZUCZYLUCZABCUOZYSFUDUEZUBZUFGKXMXMGXNUNZWTHGKUJUEZDUDUEZUBZIUUNEUD
      UEZUBZUCZUCZYBGULZUBZYGUCZYLUCZABCUOZUUNFUDUEZUBZUFYQXLUUMUUTYAUVGYPUUMUU
      SXTWTUUMUUPXQUURXSUUMUUOXPHUUMUUNXODUDGXNKUJUPZUQURUUMUUQXRIUUMUUNXOEUDUV
      HUQURUSUTUUMUVEYNUVFYOUUMUVDYMABCUUMUVCYHYLUUMUVBYDYGUUMUVAYCYBGXNVAURVBV
      BVCUUMUUNXOFUDUVHUQVDVEXKUUTXJUVGXHUUSWTXEUUPXGUURXDUUOHNUUNDUDRVFVGXFUUQ
      INUUNEUDRVFVGVHVIJUVEXIUVFJYBLUBZYEMUBZUCZYLUCZABCUOUVESUVLUVDABCUVKUVCYL
      UVIUVBUVJYGLUVAYBPVGMYFYEQVGVHVJVKVLNUUNFUDRVFVMVNVOKYRUNZYAUUEYPUULUVMXT
      UUDWTUVMXQUUAXSUUCUVMXPYTHUVMXOYSDUDKYRXNUJVPZUQURUVMXRUUBIUVMXOYSEUDUVNU
      QURUSUTUVMYNUUJYOUUKUVMYMUUIABCUVMYHUUHYLUVMYGUUGYDUVMYFUUFYEKYRVAURUTVBV
      CUVMXOYSFUDUVNUQVDVEWRWSYSUAUBZUUDUULXNUAUBZYRUAUBZUVOGXMUAVQVRZKXMUAVQVR
      ZXNYRYSYSVSZVTWATCDEFYSHIUUJYCUUFWBZOYSULZUWAUVPUVQUWBUWAUNUVRUVSXNYRYSYC
      UUFUVTYCVSUUFVSWCWAWDABCTYCUUFYKTUKZHWEZUWCIWEZUMZYBYEUMZUWCUNZUWGHWEZUWG
      IWEZUMUWFYKUWHUWIUWDUWJUWEUWGUWCHWFUWGUWCIWFWGYIUWIYJUWJYBYEHWHYBYEIWHWIW
      JWKWLWMWNWOWPWQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R q r s t u v w x y z $.  $d S q r s t u v w x y z $.
    $d X q r s t u v w x y z $.  $d Y q r s t u v w x y z $.
    $d A q r s t u v w x y z $.  $d B q r s t u v w x y z $.
    txsubsp.1 $e |- X = U. R $.
    txsubsp.2 $e |- Y = U. S $.
    $( The subspace of a topological product space induced by a subset with a
       Cartesian product representation is a topological product of the
       subspaces induced by the subspaces of the terms of the products. $)
    txsubsp $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A C_ X /\ B C_ Y ) ) ->
                      ( subSp ` <. ( A X. B ) , ( R tX S ) >. ) =
                    ( ( subSp ` <. A , R >. ) tX ( subSp ` <. B , S >. ) ) ) $=
      ( vx vy vw vr vs vt vz vu vv vq ctop wcel wa wss cxp ctx co cop csubsp
      cfv cv cin wceq wrex cab ctg wi cuni wex wb txval eleq2d ctb txbas eltg3
      syl bitrd adantr ineq1 inuni syl6eq eleq1d sseq2i stoig3 sylan2b syl2an
      tgcl an4s eleq1 bastg ad2antrr ssel2 visset eqeq1 2rexbidv elab eqid
      eqeq2d rcla4ev mpan2 ad2antrl cvv simplll inex1 a1i uniexg syl5eqel
      ssexg sylan2 ancoms ad2ant2r issubspt syl111anc mpbird ad2antll simpllr
      inex1g ad2ant2l inxp xpeq1 xpeq2 rcla42ev xpex sylibr ex biimprcd syl6
      r19.23advv imp anassrs sseldd syl5cbir r19.23adva abssdv uniopn syl11anc
      expimpd 19.23adv sylbid eleq1a r19.23adv txtop xpexg 3imtr4d xpss12
      adantl txuni sseqtr4d xpeq12 syl6eqr simprl simprr eqidd eleqtrrd
      adantlr jctild reeanv syl5ibr simpl bi2anan9 sstr impbid eqrdv ) CSTZDSTZ
      UAZAEUBZBFUBZUAZUAZIABUCZCDUDUEZUFUGUHZACUFUGUHZBDUFUGUHZUDUEZUURIUIZUVAT
      ZUVEUVDTZUURUVEJUIZUUSUJZUKZJUUTULZUVEKUIZLUIZMUIZUCZUKZMUVCULLUVBULZKUMZ
      UNUHZTZUVFUVGUURUVJUVTJUUTUURUVHUUTTZUVIUVSTZUVJUVTUOUURUWANUIZOUIZPUIZQU
      IZUCZUKZQDULPCULZOUMZUBZUVHUWCUPZUKZUAZNUQZUWBUUNUWAUWOURUUQUUNUWAUVHUWJU
      NUHZTZUWOUUNUUTUWPUVHOQPCDUSZUTUUNUWJVATZUWQUWOUROQPCDVBZNUVHUWJVCVDVEVFU
      URUWNUWBNUURUWKUWMUWBUWMUWBRUIZUVEUUSUJZUKZIUWCULZRUMZUPZUVSTZUURUWKUAZUW
      MUVIUXFUVSUWMUVIUWLUUSUJUXFUVHUWLUUSVGRIUWCUUSVHVIVJUXHUVSSTZUXEUVSUBUXGU
      URUXIUWKUULUUOUUMUUPUXIUULUUOUAZUUMUUPUAZUAZUVRVATZUXIUXJUVBSTZUVCSTZUXMU
      XKUUOUULACUPZUBUXNEUXPAGVKACVLVMZUUPUUMBDUPZUBUXOFUXRBHVKBDVLVMZKMLUVBUVC
      VBVNZUVRVOVDVPVFUXHUXDRUVSUXHUXCUXAUVSTZIUWCUXCUYAUXBUVSTUXHUVEUWCTZUAZUX
      AUXBUVSVQUYCUVRUVSUXBUURUVRUVSUBZUWKUYBUURUXMUYDUULUUOUUMUUPUXMUXTVPUVRVR
      VDVSUURUWKUYBUXBUVRTZUWKUYBUAUURUVEUWJTZUYEUWCUWJUVEVTUYFUURUVEUWGUKZQDUL
      PCULZUYEUWIUYHOUVEIWAZUWDUVEUKUWHUYGPQCDUWDUVEUWGWBWCWDUURUYHUYEUURUYGUYE
      PQCDUURUWECTZUWFDTZUAZUWGUUSUJZUVRTZUYGUYEUOUURUYLUYNUURUYLUAZUYMUVOUKZMU
      VCULLUVBULZUYNUYOUWEAUJZUVBTZUWFBUJZUVCTZUYMUYRUYTUCZUKZUYQUYOUYSUYRUVEAU
      JZUKZICULZUYJVUFUURUYKUYJUYRUYRUKZVUFUYRWEVUEVUGIUWECUVEUWEUKVUDUYRUYRUVE
      UWEAVGWFWGWHWIUYOUULUYRWJTZAWJTZUYSVUFURUULUUMUUQUYLWKVUHUYOUWEAPWAZWLWMU
      URVUIUYLUULUUOVUIUUMUUPUUOUULVUIUULUUOEWJTVUIUULUXPWJECSWNGWOAEWJWPWQWRZW
      SVFIUYRAWJCWTXAXBUYOVUAUYTUVEBUJZUKZIDULZUYKVUNUURUYJUYKUYTUYTUKZVUNUYTWE
      VUMVUOIUWFDUVEUWFUKVULUYTUYTUVEUWFBVGWFWGWHXCUYOUUMUYTWJTZBWJTZVUAVUNURUU
      LUUMUUQUYLXDUYKVUPUURUYJUWFBDXEXCUURVUQUYLUUMUUPVUQUULUUOUUPUUMVUQUUMUUPF
      WJTVUQUUMUXRWJFDSWNHWOBFWJWPWQWRZXFVFIUYTBWJDWTXAXBVUCUYOUWEUWFABXGWMUYPV
      UCUYMUYRUVNUCZUKLMUYRUYTUVBUVCUVMUYRUKUVOVUSUYMUVMUYRUVNXHWFUVNUYTUKVUSVU
      BUYMUVNUYTUYRXIWFXJXAUVQUYQKUYMUWGUUSUWEUWFVUJQWAZXKZWLUVLUYMUKUVPUYPLMUV
      BUVCUVLUYMUVOWBWCWDXLXMUYGUYEUYNUYGUXBUYMUVRUVEUWGUUSVGVJXNXOXPXQVMWQXRXS
      XTYAYBUXEUVSYCYDXTYEYFYGUVIUVSUVEYHXOYIUURUUTSTZUVEWJTZUUSWJTZUVFUVKURUUN
      VVBUUQCDUUTUUTWEZYJVFZVVCUURUYIWMUULUUOUUMUUPVVDUXJVUIVUQVVDUXKVUKVURABWJ
      WJYKVNVPZJUVEUUSWJUUTWTXAUULUUOUUMUUPUVGUVTURUXLUVDUVSUVEUXJUXNUXOUVDUVSU
      KUXKUXQUXSKMLUVBUVCUSVNUTVPYLUURUVGUWCUWHQUVCULPUVBULZOUMZUBZUVEUWLUKZUAZ
      NUQZUVFUULUUOUUMUUPUVGVVMURZUXJUXNUXOVVNUXKUXQUXSUXNUXOUAZUVGUVEVVIUNUHZT
      ZVVMVVOUVDVVPUVEOQPUVBUVCUSUTVVOVVIVATVVQVVMUROQPUVBUVCVBNUVEVVIVCVDVEVNV
      PUURVVLUVFNUURVVJVVKUVFVVKUVFUWLUVATZUURVVJUAZUVEUWLUVAVQVVSUVASTZUWCUVAU
      BZVVRUURVVTVVJUURVVBUUSUUTUPZUBVVTVVFUURUUSEFUCZVWBUUQUUSVWCUBUUNAEBFYMYN
      UUNVWBVWCUKUUQCDUUTEFVVEGHYOVFYPUUSUUTVLYDVFVVJUURVWAUURVVJVVIUVAUBVWAUUR
      VVHOUVAUURUWHUWDUVATZPQUVBUVCUURUWEUVBTZUWFUVCTZUAZUWGUVATZUWHVWDUOUURUWE
      UVMAUJZUKZLCULZUWFUVNBUJZUKZMDULZUAZUWGUVIUKZJUUTULZVWGVWHUURVWJVWMUAZMDU
      LLCULVWQVWOUURVWRVWQLMCDUURUVMCTZUVNDTZUAZVWRVWQUOUURVXAUAZVWRUVOUUTTZUWG
      UVOUUSUJZUKZUAVWQVXBVWRVXEVXCVWRVXEUOVXBVWRUWGVWIVWLUCVXDUWEVWIUWFVWLYQUV
      MUVNABXGYRWMUUNVXAVXCUUQUUNVXAUAZUVOUWPUUTVXFUWJUWPUVOUUNUWJUWPUBZVXAUUNU
      WSVXGUWTUWJVRVDVFVXFUVOUWGUKZQDULPCULZUVOUWJTVXFVWSVWTUVOUVOUKZVXIUUNVWSV
      WTYSUUNVWSVWTYTVXFUVOUUAVXHVXJUVOUVMUWFUCZUKPQUVMUVNCDUWEUVMUKUWGVXKUVOUW
      EUVMUWFXHWFUWFUVNUKVXKUVOUVOUWFUVNUVMXIWFXJXAUWIVXIOUVOUVMUVNLWAMWAXKUWDU
      VOUKUWHVXHPQCDUWDUVOUWGWBWCWDXLXSUUNUUTUWPUKVXAUWRVFUUBUUCUUDVWPVXEJUVOUU
      TUVHUVOUKUVIVXDUWGUVHUVOUUSVGWFWGXOXMXPVWJVWMLMCDUUEUUFUULUUOUUMUUPVWGVWO
      URUXJVWEVWKUXKVWFVWNUXJUULUWEWJTZVUIVWEVWKURUULUUOUUGVXLUXJVUJWMVUKLUWEAW
      JCWTXAUXKUUMUWFWJTZVUQVWFVWNURUUMUUPUUGVXMUXKVUTWMVURMUWFBWJDWTXAUUHVPUUR
      VVBUWGWJTZVVDVWHVWQURVVFVXNUURVVAWMVVGJUWGUUSWJUUTWTXAYLUWGUVAUWDYHXOXPYB
      UWCVVIUVAUUIWQWRUWCUVAYCYDXTYEYFYGUUJUUK $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R u v z $.  $d S u v z $.  $d T u v z $.  $d A u v z $.  $d B u v z $.
    txopnOLD.1 $e |- T = ( R tX S ) $.
    $( The product of two open sets is open in the product topology.  (Moved to
       ~ txopn in main set.mm and may be deleted by mathbox owner, JM. --NM
       15-Oct-2012.) $)
    txopnOLD $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. R /\ B e. S ) )
                                                      -> ( A X. B ) e. T ) $=
      ( vz vu vv ctop wcel wa cxp cv wceq wrex cab ctg cfv wss ctb txbas bastg
      syl adantr eqid xpeq1 eqeq2d xpeq2 rcla42ev mp3an3 cvv wb xpexg eqeq1
      2rexbidv elabg mpbird adantl sseldd ctx co txval syl5eq eleqtrrd ) CJKDJK
      LZACKZBDKZLZLZABMZGNZHNZINZMZOZIDPHCPZGQZRSZEVJVRVSVKVFVRVSTZVIVFVRUAKVTG
      IHCDUBVRUCUDUEVIVKVRKZVFVIWAVKVOOZIDPHCPZVGVHVKVKOZWCVKUFWBWDVKAVNMZOHIAB
      CDVMAOVOWEVKVMAVNUGUHVNBOWEVKVKVNBAUIUHUJUKVIVKULKWAWCUMABCDUNVQWCGVKULVL
      VKOVPWBHICDVLVKVOUOUPUQUDURUSUTVFEVSOVIVFCDVAVBVSEGIHCDVCFVDUEVE $.
      $( [2-Sep-2009] $)
  $}

  ${
    txcldOLD.1 $e |- T = ( R tX S ) $.
    $( The product of two closed sets is closed in the product topology.
       (Contributed by Jeff Madsen, 2-Sep-2009.)  (Moved to ~ txcld in main
       set.mm and may be deleted by mathbox owner, JM. --NM 15-Oct-2012.) $)
    txcldOLD $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. ( Clsd ` R )
                  /\ B e. ( Clsd ` S ) ) ) -> ( A X. B ) e. ( Clsd ` T ) ) $=
      ( ctop wcel wa ccld cfv cxp cuni wss cdif xpss12 ad2ant2r adantl wceq
      eqid txuni adantr sseqtr4d ex cun difeq1d difxp syl6eq txtop topopn
      ad2antlr txopn anassrs mpdan adantrr ad2antrr ancom2s adantrl unopn
      syl111anc eqeltrd adantrll adantrrl jcad iscld bi2anan9 wb syl 3imtr4d
      imp ) CGHZDGHZIZACJKHZBDJKHZIZABLZEJKHZVMACMZNZVSAOZCHZIZBDMZNZWDBOZDHZIZ
      IZVQEMZNZWJVQOZEHZIZVPVRVMWIWKWMVMWIWKVMWIIVQVSWDLZWJWIVQWONZVMVTWEWPWBWG
      AVSBWDPQRVMWJWOSZWICDEVSWDFVSTZWDTZUAZUBUCUDVMWIWMVMWCWGWMWEVMWBWGWMVTVMW
      BWGIZIZWLWAWDLZVSWFLZUEZWAWFLZUEZEXBWLWOVQOXGXBWJWOVQVMWQXAWTUBUFABVSWDUG
      UHXBEGHZXEEHZXFEHXGEHVMXHXACDEFUIZUBZXBXHXCEHZXDEHZXIXKVMWBXLWGVMWBIWDDHZ
      XLVLXNVKWBDWDWSUJUKVMWBXNXLWAWDCDEFULUMUNUOVMWGXMWBVMWGIVSCHZXMVKXOVLWGCV
      SWRUJUPVMWGXOXMVMXOWGXMVSWFCDEFULUQUMUNURXCXDEUSUTWAWFCDEFULXEXFEUSUTVAVB
      VCUDVDVKVNWCVLVOWHACVSWRVEBDWDWSVEVFVMXHVRWNVGXJVQEWJWJTVEVHVIVJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A w x y z $.  $d B w x y z $.  $d C u v w x y z $.  $d D u v w x y z $.
    $d S w $.  $d T u v w x y $.  $d J w $.  $d K w $.  $d L w $.
    $d F u v w $.  $d R w z $.
    cnresoprab.1 $e |- A = U. J $.
    cnresoprab.2 $e |- B = U. K $.
    cnresoprab.3 $e |- S = U. L $.
    cnresoprab.4 $e |- C C_ A $.
    cnresoprab.5 $e |- D C_ B $.
    cnresoprab.6 $e |- T C_ S $.
    cnresoprab.7 $e |- ( ( x e. C /\ y e. D ) -> R e. T ) $.
    cnresoprab.8 $e |- F = { <. <. x , y >. , z >. |
                                        ( ( x e. A /\ y e. B ) /\ z = R ) } $.
    cnresoprab.9 $e |- G = { <. <. x , y >. , z >. |
                                        ( ( x e. C /\ y e. D ) /\ z = R ) } $.
    cnresoprab.10 $e |- J e. Top $.
    cnresoprab.11 $e |- K e. Top $.
    cnresoprab.12 $e |- L e. Top $.
    cnresoprab.13 $e |- F e. ( ( J tX K ) Cn L ) $.
    $( Continuity of a restricted operation abstraction. $)
    cnresoprab $p |- G e. ( ( subSp ` <. ( C X. D ) , ( J tX K ) >. )
                                                Cn ( subSp ` <. T , L >. ) ) $=
      ( vw vu vv cxp cres ctx co cop csubsp cfv ccn cv wcel wa wceq copab2 wss
      resoprab2 mp2an reseq1 ax-mp 3eqtr4ri ctop wral eqid txtop pm3.2i xpss12
      csb wi ax-17 cvv wal visset hbcsb1 hbcsbg hbel hbim eleq1 anbi1d csbeq1a
      csbeq2dv mpan2 eleq1d imbi12d anbi2d chvar sseli oprabval2gf 3expia
      syl2an mpd eqeltrd rgen2 fveq2 df-opr syl6eqr ralxp mpbir cuni txuni
      eqcomi cnres2 mp3an eqeltri ) LKFGULZUMZXNMNUNUOZUPUQURJOUPUQURUSUOZAUTZD
      VABUTZEVAVBCUTHVCZVBABCVDZXNUMZXRFVAZXSGVAZVBZXTVBABCVDZXOLFDVEZGEVEZYBYF
      VCSTXTABCDEFGVFVGKYAVCXOYBVCUCKYAXNVHVIUDVJXPVKVAZOVKVAZVBXNDEULZVEZJIVEZ
      VBKXPOUSUOVAZUIUTZKURZJVAZUIXNVLZVBXOXQVAYIYJMVKVAZNVKVAZYIUEUFMNXPXPVMZV
      NVGUGVOYLYMYGYHYLSTFDGEVPVGUAVOYNYRUHYRUJUTZUKUTZKUOZJVAZUKGVLUJFVLUUEUJU
      KFGUUBFVAZUUCGVAZVBZUUDBUUCAUUBHVQZVQZJUUHUUJJVAZUUDUUJVCZYCUUGVBZBUUCHVQ
      ZJVAZVRZUUHUUKVRAUJUUHUUKAUUHAVSAUIUIUUJJUUCVTVAZYOUUJVAZUURAWAVRUKWBZABU
      IUUCUUIVTYOUUCVAZAVSAUIUUBHUJWBYOUUBVAAVSWCZWDVIYOJVAZAVSWEWFXRUUBVCZUUMU
      UHUUOUUKUVCYCUUFUUGXRUUBFWGWHUVCUUNUUJJUVCUUQUUNUUJVCUUSUVCBUUCHUUIVTAUUB
      HWIZWJWKWLWMYEHJVAZVRUUPBUKUUMUUOBUUMBVSBUIUIUUNJBUIUUCHUUSUUTBVSZWCUVBBV
      SWEWFXSUUCVCZYEUUMUVEUUOUVGYDUUGYCXSUUCGWGWNUVGHUUNJBUUCHWIWLWMUBWOWOZUUF
      UUBDVAZUUCEVAZUUKUULVRUUGFDUUBSWPGEUUCTWPUVIUVJUUKUULABCUIUUBUUCDEHUUJKUU
      IJUVABUIUUCUUIUUSUVFWCUVDBUUCUUIWIUCWQWRWSWTUVHXAXBYQUUEUIUJUKFGYOUUBUUCU
      PZVCZYPUUDJUVLYPUVKKURUUDYOUVKKXCUUBUUCKXDXEWLXFXGVOUIXNJKXPOYKIXPXHZYKYS
      YTUVMYKVCUEUFMNXPDEUUAPQXIVGXJRXKXLXM $.
      $( [11-Jun-2010] $)

    cnresoprab.14 $e |- M = ( subSp ` <. C , J >. ) $.
    cnresoprab.15 $e |- N = ( subSp ` <. D , K >. ) $.
    cnresoprab.16 $e |- O = ( subSp ` <. T , L >. ) $.
    $( Continuity of a restricted operation abstraction. $)
    cnresoprab2 $p |- G e. ( ( M tX N ) Cn O ) $=
      ( cxp ctx co cop csubsp cfv ccn cnresoprab opreq12i ctop wcel wss wceq
      txsubsp mp4an eqtr4i eleqtrri ) LFGUOMNUPUQURUSUTZJOURUSUTZVAUQPQUPUQZRVA
      UQABCDEFGHIJKLMNOSTUAUBUCUDUEUFUGUHUIUJUKVBVNVLRVMVAVNFMURUSUTZGNURUSUTZU
      PUQZVLPVOQVPUPULUMVCMVDVENVDVEFDVFGEVFVLVQVGUHUIUBUCFGMNDESTVHVIVJUNVCVK
      $.
      $( [15-Jun-2010] $)
  $}

  ${
    $d A v x z $.  $d A x y $.  $d A w $.  $d J v w $.  $d K v x w $.
    $d L v x w u $.  $d F u v w $.  $d G u v w $.  $d R y $.  $d R w $.
    $d S z $.  $d S w $.  $d P x w $.
    cnopropabco.1 $e |- A = U. J $.
    cnopropabco.2 $e |- J e. Top $.
    cnopropabco.3 $e |- K e. Top $.
    cnopropabco.4 $e |- L e. Top $.
    cnopropabco.5 $e |- M e. Top $.
    cnopropabco.6 $e |- ( x e. A -> R e. X ) $.
    cnopropabco.7 $e |- ( x e. A -> S e. Y ) $.
    cnopropabco.8 $e |- F = { <. x , y >. | ( x e. A /\ y = R ) } $.
    cnopropabco.9 $e |- G = { <. x , z >. | ( x e. A /\ z = S ) } $.
    cnopropabco.10 $e |- H = { <. x , w >. | ( x e. A /\ w = ( R P S ) ) } $.
    cnopropabco.11 $e |- F e. ( J Cn K ) $.
    cnopropabco.12 $e |- G e. ( J Cn L ) $.
    cnopropabco.13 $e |- P e. ( ( K tX L ) Cn M ) $.
    $( The composition of a continuous operation with two continuous functions
       is continuous. $)
    cnopropabco $p |- H e. ( J Cn M ) $=
      ( vv vu cv wcel cfv cop wceq wa copab ccom ccn co cuni cxp wfn wf ctx
      ctop eqid txtop mp2an txuni eqcomi cnf mp3an ffn ax-mp fvopab2 mpdan
      fveq1i syl5eq ffvelrni eqeltrrd opeq12d eqeq2d pm5.32i opabbii opropabco
      w3a 3pm3.2i ax-17 hbopab1 hbxfr hbfv hbop hbeq hban eleq1 fveq2 anbi12d
      cbvopab1 pm3.2i txcnopab eqeltri cnco ) KFAUMZEUNZDUMZXFIUOZXFJUOZUPZUQZU
      RZADUSZUTZLOVAVBZFMVCZNVCZVDZVEZKXOUQXSOVCZFVFZXTMNVGVBZVHUNZOVHUNZFYCOVA
      VBUNZYBMVHUNZNVHUNZYDTUAMNYCYCVIZVJVKZUBUJFYCOXSYAYCVCZXSYGYHYKXSUQTUAMNY
      CXQXRYIXQVIZXRVIZVLVKVMYAVIVNVOXSYAFVPVQADEGHXQXRXNKFXGXIGXQXGXFXGBUMGUQU
      RZABUSZUOZGXIXGGPUNYPGUQUCABEGPVRVSXFIYOUEVTWAZEXQXFILVHUNZYGILMVAVBUNZEX
      QIVFSTUHILMEXQRYLVNVOWBWCXGXJHXRXGXFXGCUMHUQURZACUSZUOZHXJXGHQUNUUBHUQUDA
      CEHQVRVSXFJUUAUFVTWAZEXRXFJYRYHJLNVAVBUNZEXRJVFSUAUIJLNEXRRYMVNVOWBWCXMXG
      XHGHUPZUQZURADXGXLUUFXGXKUUEXHXGXIGXJHYQUUCWDWEWFWGUGWHVQYRYDYEWIXNLYCVAV
      BZUNZYFURXOXPUNYRYDYESYJUBWJUUHYFXNUKUMZEUNZXHUUIIUOZUUIJUOZUPZUQZURZUKDU
      SZUUGXMUUOADUKXMUKWKUUJUUNAUUJAWKAULULXHUUMULUMZXHUNAWKAULUUKUULAULUUIIAU
      LIYOUEYNABULWLWMUUQUUIUNAWKZWNAULUUIJAULJUUAUFYTACULWLWMUURWNWOWPWQXFUUIU
      QZXGUUJXLUUNXFUUIEWRUUSXKUUMXHUUSXIUUKXJUULXFUUIIWSXFUUIJWSWDWEWTXAYGYHYR
      WIYSUUDURUUPUUGUNYGYHYRTUASWJYSUUDUHUIXBUKDMNYCLIJUUPEYIRUUPVIXCVKXDUJXBX
      NFLYCOXEVKXD $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d J x w $.  $d F w $.  $d G w $.  $d R y w $.  $d S z w $.  $d P x w $.
    $d x y z w $.
    cnopropabcoc.1 $e |- J = ( MetOpen ` ( abs o. - ) ) $.
    cnopropabcoc.2 $e |- R e. _V $.
    cnopropabcoc.3 $e |- S e. _V $.
    cnopropabcoc.4 $e |- F = { <. x , y >. | ( x e. CC /\ y = R ) } $.
    cnopropabcoc.5 $e |- G = { <. x , z >. | ( x e. CC /\ z = S ) } $.
    cnopropabcoc.6 $e |- H = { <. x , w >. | ( x e. CC /\ w = ( R P S ) ) } $.
    cnopropabcoc.7 $e |- F e. ( CC -cn-> CC ) $.
    cnopropabcoc.8 $e |- G e. ( CC -cn-> CC ) $.
    cnopropabcoc.9 $e |- P e. ( ( J tX J ) Cn J ) $.
    $( The composition of a continuous operation with two continuous functions
       is continuous.  Version for functions ` CC --> CC ` . $)
    cnopropabcoc $p |- H e. ( CC -cn-> CC ) $=
      ( ccn co cc ccncf cvv cuni cabs cmin ccom cme wcel wceq eqid cnmet
      cnmetba uniopn2 ax-mp eqcomi ctop opntop cv a1i cncfmet1 eleqtri
      cnopropabco eleqtrri ) JKKUAUBZUCUCUDUBZABCDUCEFGHIJKKKKUEUEKUFZUCUGUHUIZ
      UJUKZVIUCULVJVJUMZUNZVJKUCVJVLUOLUPUQURVKKUSUKVMVJKLUTUQZVNVNVNFUEUKAVAUC
      UKZMVBGUEUKVONVBOPQHVHVGRVJKVLLVCZVDIVHVGSVPVDTVEVPVF $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d J s t w x y $.  $d K s t w x y $.  $d L s t w x y z $.
    $d M s t w x y z $.  $d A s t u v w x y z $.  $d B s t u v w x y z $.
    $d R u w z $.  $d S v w z $.  $d F r s t w z $.  $d G r s t w z $.
    $d H w $.  $d P w x y z $.  $d r x y $.
    cnoproprabco.1 $e |- A = U. J $.
    cnoproprabco.2 $e |- B = U. K $.
    cnoproprabco.3 $e |- J e. Top $.
    cnoproprabco.4 $e |- K e. Top $.
    cnoproprabco.5 $e |- L e. Top $.
    cnoproprabco.6 $e |- M e. Top $.
    cnoproprabco.7 $e |- N e. Top $.
    cnoproprabco.8 $e |- ( ( x e. A /\ y e. B ) -> R e. X ) $.
    cnoproprabco.9 $e |- ( ( x e. A /\ y e. B ) -> S e. Y ) $.
    cnoproprabco.10 $e |- F = { <. <. x , y >. , u >. |
                                        ( ( x e. A /\ y e. B ) /\ u = R ) } $.
    cnoproprabco.11 $e |- G = { <. <. x , y >. , v >. |
                                        ( ( x e. A /\ y e. B ) /\ v = S ) } $.
    cnoproprabco.12 $e |- H = { <. <. x , y >. , z >. |
                                ( ( x e. A /\ y e. B ) /\ z = ( R P S ) ) } $.
    cnoproprabco.13 $e |- F e. ( ( J tX K ) Cn L ) $.
    cnoproprabco.14 $e |- G e. ( ( J tX K ) Cn M ) $.
    cnoproprabco.15 $e |- P e. ( ( L tX M ) Cn N ) $.
    $( The composition of a continuous operation with two continuous operations
       is continuous. $)
    cnoproprabco $p |- H e. ( ( J tX K ) Cn N ) $=
      ( vw vs vt vr cv wcel wa co cop wceq copab2 ccom ctx ccn cuni cxp wfn wf
      ctop eqid txtop mp2an txuni eqcomi cnf mp3an ffn ax-mp oprabval4g
      mpd3an3 foprcl eqeltrrd opeq12d eqeq2d pm5.32i oprabbii oprab2co w3a
      3pm3.2i ax-17 hboprab1 hbxfr hbopr hbop hbeq hban hboprab2 eleq1
      bi2anan9 opreq12 anbi12d cbvoprab12 pm3.2i txcnoprab eqeltri cnco ) MHAUT
      ZFVAZBUTZGVAZVBZUPUTZXLXNKVCZXLXNLVCZVDZVEZVBZABUPVFZVGZNOVHVCZRVIVCZHPVJ
      ZQVJZVKZVLZMYDVEYIRVJZHVMZYJPQVHVCZVNVAZRVNVAZHYMRVIVCVAZYLPVNVAZQVNVAZYN
      UEUFPQYMYMVOZVPVQZUGUOHYMRYIYKYMVJZYIYQYRUUAYIVEUEUFPQYMYGYHYSYGVOZYHVOZV
      RVQVSYKVOVTWAYIYKHWBWCABUPCFGIJYGYHYCMHXPXRIYGXMXOISVAXRIVEUHABEFGISKUJWD
      WEZXLXNYGFGKYEVNVAZYQKYEPVIVCVAZFGVKZYGKVMNVNVAZOVNVAZUUEUCUDNOYEYEVOZVPV
      QZUEUMKYEPUUGYGYEVJZUUGUUHUUIUULUUGVEUCUDNOYEFGUUJUAUBVRVQVSZUUBVTWAWFWGX
      PXSJYHXMXOJTVAXSJVEUIABDFGJTLUKWDWEZXLXNYHFGLUUEYRLYEQVIVCVAZUUGYHLVMUUKU
      FUNLYEQUUGYHUUMUUCVTWAWFWGYBXPXQIJVDZVEZVBABUPXPYAUUQXPXTUUPXQXPXRIXSJUUD
      UUNWHWIWJWKULWLWCUUEYNYOWMYCYEYMVIVCZVAZYPVBYDYFVAUUEYNYOUUKYTUGWNUUSYPYC
      UQUTZFVAZURUTZGVAZVBZXQUUTUVBKVCZUUTUVBLVCZVDZVEZVBZUQURUPVFZUURYBUVIABUP
      UQURYBUQWOYBURWOUVDUVHAUVDAWOAUSUSXQUVGUSUTZXQVAZAWOAUSUVEUVFAUSUUTUVBKUV
      KUUTVAZAWOZAUSKXPEUTIVEVBZABEVFZUJUVOABEUSWPWQUVKUVBVAZAWOZWRAUSUUTUVBLUV
      NAUSLXPDUTJVEVBZABDVFZUKUVSABDUSWPWQUVRWRWSWTXAUVDUVHBUVDBWOBUSUSXQUVGUVL
      BWOBUSUVEUVFBUSUUTUVBKUVMBWOZBUSKUVPUJUVOABEUSXBWQUVQBWOZWRBUSUUTUVBLUWAB
      USLUVTUKUVSABDUSXBWQUWBWRWSWTXAXLUUTVEZXNUVBVEZVBZXPUVDYAUVHUWCXMUVAUWDXO
      UVCXLUUTFXCXNUVBGXCXDUWEXTUVGXQUWEXRUVEXSUVFXLUUTXNUVBKXEXLUUTXNUVBLXEWHW
      IXFXGYQYRVBUUHUUIVBUUFUUOVBUVJUURVAYQYRUEUFXHUUHUUIUCUDXHUUFUUOUMUNXHUQUR
      UPPQYMNKLUVJOFGYEYSUAUBUUJUVJVOXIWAXJUOXHYCHYEYMRXKVQXJ $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d J x y z $.  $d R u z $.  $d S v z $.  $d F z $.  $d G z $.
    $d P x y z $.  $d u v x y $.
    cnoproprabcoc.1 $e |- J = ( MetOpen ` ( abs o. - ) ) $.
    cnoproprabcoc.2 $e |- R e. _V $.
    cnoproprabcoc.3 $e |- S e. _V $.
    cnoproprabcoc.4 $e |- F = { <. <. x , y >. , u >. |
                                      ( ( x e. CC /\ y e. CC ) /\ u = R ) } $.
    cnoproprabcoc.5 $e |- G = { <. <. x , y >. , v >. |
                                      ( ( x e. CC /\ y e. CC ) /\ v = S ) } $.
    cnoproprabcoc.6 $e |- H = { <. <. x , y >. , z >. |
                              ( ( x e. CC /\ y e. CC ) /\ z = ( R P S ) ) } $.
    cnoproprabcoc.7 $e |- F e. ( ( J tX J ) Cn J ) $.
    cnoproprabcoc.8 $e |- G e. ( ( J tX J ) Cn J ) $.
    cnoproprabcoc.9 $e |- P e. ( ( J tX J ) Cn J ) $.
    $( The composition of a continuous operation with two continuous operations
       is continuous.  Version for operations ` ( CC X. CC ) --> CC ` . $)
    cnoproprabcoc $p |- H e. ( ( J tX J ) Cn J ) $=
      ( cc cvv cuni cabs cmin ccom cme wcel wceq eqid cnmet cnmetba uniopn2
      ax-mp eqcomi ctop opntop cv wa a1i cnoproprabco ) ABCDEUBUBFGHIJKLLLLLUCU
      CLUDZUBUEUFUGZUHUIZVCUBUJVDVDUKZULZVDLUBVDVFUMMUNUOUPZVHVELUQUIVGVDLMURUO
      ZVIVIVIVIGUCUIAUSUBUIBUSUBUIUTZNVAHUCUIVJOVAPQRSTUAVB $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d A u v w x y z $.  $d B u v x y z $.  $d F t u v y z $.  $d G v $.
    $d R u w $.  $d t w x $.
    cnoprab.1 $e |- A = U. J $.
    cnoprab.2 $e |- B = U. K $.
    cnoprab.3 $e |- J e. Top $.
    cnoprab.4 $e |- K e. Top $.
    cnoprab.5 $e |- L e. Top $.
    cnoprab.6 $e |- ( x e. A -> R e. X ) $.
    cnoprab.7 $e |- F = { <. x , w >. | ( x e. A /\ w = R ) } $.

    ${
      cnoprab1.8 $e |- G = { <. <. x , y >. , z >. |
                                        ( ( x e. A /\ y e. B ) /\ z = R ) } $.
      cnoprab1.9 $e |- F e. ( J Cn L ) $.
      $( Continuity of an operation which is a function in only the first
         variable. $)
      cnoprab1 $p |- G e. ( ( J tX K ) Cn L ) $=
        ( vu vv vt c1st cxp cres ccom ctx co ccn wfn wceq cuni wf ctop wcel
        eqid cnf mp3an ffn ax-mp cv simpl wa copab2 cvv wss wfo fo1st fofn ssv
        fnssres mp2an fnoprv mpbi oprvres cop cfv df-opr visset op1st eqtri
        syl6eq eqeq2d pm5.32i oprabbii ax-17 copab hbopab1 hbxfr hbfv hbeq
        hban eleq1 anbi1d wb fveq2 fvopab2 mpdan fveq1i syl5eq sylan9eq ex
        adantrd pm5.32d bitrd cbvoprab1 eqtr4i oprabco w3a txtop 3pm3.2i tx1cn
        pm3.2i cnco eqeltri ) IHUFEFUGZUHZUIZJKUJUKZLULUKZHEUMZIYAUNELUOZHUPZYD
        JUQURZLUQURZHJLULUKURZYFPRUBHJLEYENYEUSUTVAEYEHVBVCUCBUDCEFUCVDZEXTIHYJ
        EURZBVDZFURZVEXTYKYMVFZUDVDZYJYLXTUKZUNZVFZUCBUDVGZYNYOYJUNZVFZUCBUDVGX
        TXSUMZXTYSUNUFVHUMZXSVHVIUUBVHVHUFVJUUCVKVHVHUFVLVCXSVMVHXSUFVNVOUCBUDE
        FXTVPVQYRUUAUCBUDYNYQYTYNYPYJYOYNYPYJYLUFUKZYJYJYLEFUFVRUUDYJYLVSUFVTYJ
        YJYLUFWAYJYLUCWBWCWDWEWFWGWHWDIAVDZEURZYMVFZCVDZGUNZVFZABCVGYNUUHYJHVTZ
        UNZVFZUCBCVGUAUUMUUJUCBCAYNUULAYNAWIAUEUEUUHUUKUEVDZUUHURAWIAUEYJHAUEHU
        UFDVDGUNVFZADWJZTUUOADUEWKWLUUNYJURAWIWMWNWOUUJUCWIYJUUEUNZUUMUUGUULVFU
        UJUUQYNUUGUULUUQYKUUFYMYJUUEEWPWQWQUUQUUGUULUUIUUQUUFUULUUIWRZYMUUQUUFU
        URUUQUUFVFUUKGUUHUUQUUFUUKUUEHVTZGYJUUEHWSUUFUUEUUPVTZGUUSUUFGMURUUTGUN
        SADEGMWTXAUUEHUUPTXBXCXDWFXEXFXGXHXIXJXKVCYBUQURZYGYHXLXTYBJULUKURZYIVF
        YAYCURUVAYGYHYGKUQURZUVAPQJKYBYBUSZXMVOPRXNUVBYIYGUVCUVBPQJKYBEFXSUVDNO
        XSUSXOVOUBXPXTHYBJLXQVOXR $.
        $( [11-Jun-2010] $)
    $}

    ${
      cnoprab2.8 $e |- G = { <. <. y , x >. , z >. |
                                        ( ( y e. B /\ x e. A ) /\ z = R ) } $.
      cnoprab2.9 $e |- F e. ( J Cn L ) $.
      $( Continuity of an operation which is a function in only the second
         variable. $)
      cnoprab2 $p |- G e. ( ( K tX J ) Cn L ) $=
        ( vu vv vt c2nd cxp cres ccom ctx co ccn wfn wceq cuni wf ctop wcel
        eqid cnf mp3an ffn ax-mp cv simpr wa copab2 cvv wss wfo fo2nd fofn ssv
        fnssres mp2an fnoprv mpbi oprvres cop cfv df-opr visset op2nd eqtri
        syl6eq eqeq2d pm5.32i oprabbii ax-17 copab hbopab1 hbxfr hbfv hbeq
        hban eleq1 anbi2d anbi1d wb fveq2 fvopab2 mpdan fveq1i syl5eq sylan9eq
        ex adantld pm5.32d bitrd cbvoprab2 eqtr4i oprabco w3a txtop 3pm3.2i
        tx2cn pm3.2i cnco eqeltri ) IHUFFEUGZUHZUIZKJUJUKZLULUKZHEUMZIYBUNELUOZ
        HUPZYEJUQURZLUQURZHJLULUKURZYGPRUBHJLEYFNYFUSUTVAEYFHVBVCBUCUDCFEUCVDZE
        YAIHBVDZFURZYKEURZVEYAYMYNVFZUDVDZYLYKYAUKZUNZVFZBUCUDVGZYOYPYKUNZVFZBU
        CUDVGYAXTUMZYAYTUNUFVHUMZXTVHVIUUCVHVHUFVJUUDVKVHVHUFVLVCXTVMVHXTUFVNVO
        BUCUDFEYAVPVQYSUUBBUCUDYOYRUUAYOYQYKYPYOYQYLYKUFUKZYKYLYKFEUFVRUUEYLYKV
        SUFVTYKYLYKUFWAYLYKBWBUCWBWCWDWEWFWGWHWDIYMAVDZEURZVFZCVDZGUNZVFZBACVGY
        OUUIYKHVTZUNZVFZBUCCVGUAUUNUUKBUCCAYOUUMAYOAWIAUEUEUUIUULUEVDZUUIURAWIA
        UEYKHAUEHUUGDVDGUNVFZADWJZTUUPADUEWKWLUUOYKURAWIWMWNWOUUKUCWIYKUUFUNZUU
        NUUHUUMVFUUKUURYOUUHUUMUURYNUUGYMYKUUFEWPWQWRUURUUHUUMUUJUURUUGUUMUUJWS
        ZYMUURUUGUUSUURUUGVFUULGUUIUURUUGUULUUFHVTZGYKUUFHWTUUGUUFUUQVTZGUUTUUG
        GMURUVAGUNSADEGMXAXBUUFHUUQTXCXDXEWFXFXGXHXIXJXKXLVCYCUQURZYHYIXMYAYCJU
        LUKURZYJVFYBYDURUVBYHYIKUQURZYHUVBQPKJYCYCUSZXNVOPRXOUVCYJUVDYHUVCQPKJY
        CFEXTUVEONXTUSXPVOUBXQYAHYCJLXRVOXS $.
        $( [11-Jun-2010] $)
    $}
  $}

  ${
    $d F y z $.  $d R w $.  $d w x y z $.
    cnoprabc.1 $e |- J = ( MetOpen ` ( abs o. - ) ) $.
    cnoprabc.2 $e |- R e. _V $.
    cnoprabc.3 $e |- F = { <. x , w >. | ( x e. CC /\ w = R ) } $.
    cnoprabc.4 $e |- F e. ( CC -cn-> CC ) $.

    ${
      cnoprab1c.5 $e |- G = { <. <. x , y >. , z >. |
                                      ( ( x e. CC /\ y e. CC ) /\ z = R ) } $.
      $( Continuity of an operation which is a function in only the first
         variable.  Version for operations ` ( CC X. CC ) --> CC ` . $)
      cnoprab1c $p |- G e. ( ( J tX J ) Cn J ) $=
        ( cc cvv cuni cabs cmin ccom cme wcel wceq eqid cnmet cnmetba uniopn2
        ax-mp eqcomi ctop opntop cv a1i ccncf co ccn cncfmet1 eleqtri cnoprab1
        ) ABCDNNEFGHHHOHPZNQRSZTUAZUSNUBUTUTUCZUDZUTHNUTVBUEIUFUGUHZVDVAHUIUAVC
        UTHIUJUGZVEVEEOUAAUKNUAJULKMFNNUMUNHHUOUNLUTHVBIUPUQUR $.
        $( [11-Jun-2010] $)
    $}

    ${
      cnoprab2c.5 $e |- G = { <. <. y , x >. , z >. |
                                      ( ( y e. CC /\ x e. CC ) /\ z = R ) } $.
      $( Continuity of an operation which is a function in only the second
         variable.  Version for operations ` ( CC X. CC ) --> CC ` . $)
      cnoprab2c $p |- G e. ( ( J tX J ) Cn J ) $=
        ( cc cvv cuni cabs cmin ccom cme wcel wceq eqid cnmet cnmetba uniopn2
        ax-mp eqcomi ctop opntop cv a1i ccncf co ccn cncfmet1 eleqtri cnoprab2
        ) ABCDNNEFGHHHOHPZNQRSZTUAZUSNUBUTUTUCZUDZUTHNUTVBUEIUFUGUHZVDVAHUIUAVC
        UTHIUJUGZVEVEEOUAAUKNUAJULKMFNNUMUNHHUOUNLUTHVBIUPUQUR $.
        $( [11-Jun-2010] $)
    $}
  $}

  ${
    $d M b c d s t u v w x y z $.  $d N b c d s t u v x y z $.
    $d X b c d r s t u v w x y z $.  $d Y b c d r s t u v x y z $.
    $d R a r s t u v x y z $.  $d S a r s t u v x y z $.
    $d U a r s t u v x y z $.
    txmet.1 $e |- R = ( MetOpen ` M ) $.
    txmet.2 $e |- S = ( MetOpen ` N ) $.
    txmet.3 $e |- T = ( R tX S ) $.
    txmet.4 $e |- X = dom dom M $.
    txmet.5 $e |- Y = dom dom N $.
    txmet.6 $e |- M e. Met $.
    txmet.7 $e |- N e. Met $.
    txmet.8 $e |- U = { <. <. b , c >. , d >. | ( ( b e. ( X X. Y ) /\
              c e. ( X X. Y ) ) /\ d = sup ( { ( ( 1st ` b ) M ( 1st ` c ) ) ,
                              ( ( 2nd ` b ) N ( 2nd ` c ) ) } , RR , < ) ) } $.
    $( The product of two metric space topologies, in terms of the sup norm. $)
    txmet $p |- T = ( MetOpen ` U ) $=
      ( vz vu vv vx vy va vr vs vt vw cv cxp wceq wrex cab ctg cfv cbl crn
      copn ctx co ctop wcel cme opntop ax-mp txval mp2an eqtri wss wa wi wral
      cuni unieqi eqid uniopn2 eqcomi txuni ctb txbas unitg 3eqtr3ri eleq2i
      visset eqeq1 2rexbidv elab eleq2 sseq2 anbi2d rexbidv imbi12d cc0 clt
      wbr cr cop wex opreq1 sseq1d opnss mpan ssel2 sylan wb isopn4 simprbi
      rcla4cva jca cle cif ifcl ad2ant2l ad2ant2r id ad2ant2lr 0re ltmin
      mp3an1 biimpar an4s syl2an an4 jctl ad3antrrr adantr adantll min1
      ad2antlr ssbl syl31anc sstr2 syl adantl ad2antrr min2 c2nd c1st metcl
      anim12i w3a wn ltnle ancoms 3adant3 ifbid ifswap syl6eq breq1d maxlt
      bitrd 3expa adantrr an32s pm5.32da biimpd syl6ib anim2d elxp6 anbi1i
      anass bitri syl5ib opelxpi metxp cdm cpr csup ltso supex dmoprab2 dmeqi
      dmxpid eqtr2i elbl mpanl1 op1st op2nd metxpdval ex pm5.32d adantlr
      anbi12d syl5bb 3imtr4d sylan2 anassrs ssrdv xpss12 syl5 syl2and impr
      syl2anb breq2 opreq2 rcla4ev syl12anc r19.23advv reeanv syl5ibr imp
      syl5cbir com23 imp3a 19.23advv elxp blssex mpbird rgen2 blbas unirnbl
      eqtr3i 3eqtr2ri tgss2 mp3an mpbir crab blrn2 eleq1 xp1st blopn xp2nd wal
      fveq2 opreq1d eleq2d bibi12d elbl2 mp3anl1 bicomd bi2anan9 expcom anidms
      com12 3bitrd exp3a impcom blssm sseld anim12d pm4.71rd bitr4d syl5bir
      expdimp 19.23aivv sylbi elrab opelxp 3bitr4g 19.21aivv wrel ssrab2 relxp
      relss mp2 eqrel sylibr xpeq1 eqeq2d xpeq2 rcla42ev syl111anc cvv
      elisseti dmex eqeltri xpex rabex expimpd r19.23aivv ssriv tgss eqssi
      tgbl 3eqtri ) CTUJZUAUJZUBUJZUKZULZUBBUMUAAUMZTUNZUOUPZDUQUPZURZUOUPZDUSU
      PZCABUTVAZVWDNAVBVCZBVBVCZVWIVWDULEVDVCZVWJQEALVEVFZFVDVCZVWKRFBMVEVFZTUB
      UAABVGVHZVIVWDVWGVWDVWGVJZUCUJZUDUJZVCZVWRUEUJZVCZVXAVWSVJZVKZUEVWFUMZVLZ
      UDVWCVMUCVWCVNZVMZVXFUCUDVXGVWCVWRVXGVCVWRGHUKZVCZVWSVVTULZUBBUMUAAUMZVXF
      VWSVWCVCVXGVXIVWRVWIVNZVWDVNZVXIVXGVWIVWDVWPVOZVWJVWKVXMVXIULVWMVWOABVWIG
      HVWIVPAVNZGVWLVXPGULQEAGOLVQVFVRBVNZHVWNVXQHULRFBHPMVQVFVRVSVHZVWCVTVCZVX
      NVXGULVWJVWKVXSVWMVWOTUBUAABWAVHZVWCWBVFZWCWDVWBVXLTVWSUDWEVVQVWSULVWAVXK
      UAUBABVVQVWSVVTWFWGWHVXJVXLVXFVXJVXKVXFUAUBABVXJVVRAVCZVVSBVCZVKZVXKVXFVL
      VXKVXFVWRVVTVCZVXBVXAVVTVJZVKZUEVWFUMZVLVXJVYDVKZVXKVWTVYEVXEVYHVWSVVTVWR
      WIVXKVXDVYGUEVWFVXKVXCVYFVXBVWSVVTVXAWJWKWLWMVYIVYEVYHVYIVYEVKVYHWNUFUJZW
      OWPZVWRVYJVWEVAZVVTVJZVKZUFWQUMZVYIVYEVYOVYIVWRUGUJZUHUJZWRZULZVYPVVRVCZV
      YQVVSVCZVKZVKZUHWSUGWSVYOVYEVYIWUCVYOUGUHVYDWUCVYOVLVXJVYDVYSWUBVYOVYDWUB
      VYSVYOVYDWUBVYSVYOVLZVYBVYTVYCWUAWUDVYSVYOVYKVYRVYJVWEVAZVVTVJZVKZUFWQUMZ
      VYBVYTVKZVYCWUAVKZVKVYSVYNWUGUFWQVYSVYMWUFVYKVYSVYLWUEVVTVWRVYRVYJVWEWTXA
      WKWLWUIVYPGVCZWNVWRWOWPZVYPVWREUQUPZVAZVVRVJZVKZUCWQUMZVKVYQHVCZWNVWSWOWP
      ZVYQVWSFUQUPZVAZVVSVJZVKZUDWQUMZVKWUHWUJWUIWUKWUQVYBVVRGVJZVYTWUKVWLVYBWV
      EQVVREAGOLXBXCVVRGVYPXDXEVYBWULUIUJZVWRWUMVAZVVRVJZVKZUCWQUMZUIVVRVMZVYTW
      UQVYBWVEWVKVWLVYBWVEWVKVKXFQUIUCVVREAGOLXGVFXHWVJWUQUIVYPVVRWVFVYPULZWVIW
      UPUCWQWVLWVHWUOWULWVLWVGWUNVVRWVFVYPVWRWUMWTXAWKWLXIXEXJWUJWURWVDVYCVVSHV
      JZWUAWURVWNVYCWVMRVVSFBHPMXBXCVVSHVYQXDXEVYCWUSVVQVWSWUTVAZVVSVJZVKZUDWQU
      MZTVVSVMZWUAWVDVYCWVMWVRVWNVYCWVMWVRVKXFRTUDVVSFBHPMXGVFXHWVQWVDTVYQVVSVV
      QVYQULZWVPWVCUDWQWVSWVOWVBWUSWVSWVNWVAVVSVVQVYQVWSWUTWTXAWKWLXIXEXJWUKWUR
      WUQWVDWUHWUKWURVKZWUQWVDVKZWUHWVTWUPWVCVKZUDWQUMUCWQUMWUHWWAWVTWWBWUHUCUD
      WQWQWVTVWRWQVCZVWSWQVCZVKZWWBWUHVLZWUKWWCWURWWDWWFWUKWWCVKZWURWWDVKZVKZWW
      BWUHWWGWUPWWHWVCWUHWWGWUPVKZWWHWVCVKZVKVWRVWSXKWPZVWRVWSXLZWQVCZWNWWMWOWP
      ZVYRWWMVWEVAZVVTVJZWUHWWGWWHWWNWUPWVCWWCWWDWWNWUKWURWWLVWRVWSWQXMZXNXOWWJ
      WWCWULVKZWWDWUSVKZWWOWWKWWCWULWWSWUKWUOWWSXPZXQWWDWUSWWTWURWVBWWTXPZXQWWC
      WWDWULWUSWWOWWEWWOWULWUSVKZWNWQVCWWCWWDWWOWXCXFXRWNVWRVWSXSXTYAZYBYCWWGWW
      HWUPWVCWWQWWIWVTWWEVKZWXCWUOWVBVKZVKWWQWWBWUKWWCWURWWDYDWULWUOWUSWVBYDWXE
      WXCWXFWWQWXEWXCVKZWUOVYPWWMWUMVAZVVRVJZWVBVYQWWMWUTVAZVVSVJZWWQWXGWXHWUNV
      JZWUOWXIVLWXGVWLWUKVKZWWNWWOVKZWWSWWMVWRXKWPZWXLWUKWXMWURWWEWXCWUKVWLQYEY
      FWWEWXCWXNWVTWWEWXCVKZWWNWWOWWEWWNWXCWWRYGWXDXJZYHZWWEWXCWWSWVTWWCWULWWSW
      WDWUSWXAXOYHWWEWXOWVTWXCVWRVWSYIYJEVYPWWMVWRGOYKYLWXHWUNVVRYMYNWXGWXJWVAV
      JZWVBWXKVLWXGVWNWURVKZWXNWWTWWMVWSXKWPZWXSWVTWXTWWEWXCWURWXTWUKWURVWNRYEY
      OYPWXRWWEWXCWWTWVTWWDWUSWWTWWCWULWXBXNYHWWEWYAWVTWXCVWRVWSYQYJFVYQWWMVWSH
      PYKYLWXJWVAVVSYMYNWXGWXHWXJUKZVVTVJZWWQWXIWXKVKWXGWWPWYBVJWYCWWQVLWXGTWWP
      WYBWVTWWEWXCVVQWWPVCZVVQWYBVCZVLZWXPWVTWXNWYFWXQWVTWXNVKZVVQVXIVCZVYQVVQY
      RUPZFVAZVYPVVQYSUPZEVAZWOWPZWYLWYJXLZWWMWOWPZVKZVVQWYKWYIWRULZWYKGVCZWYLW
      WMWOWPZVKZWYIHVCZWYJWWMWOWPZVKZVKZVKZWYDWYEWYGWYQWYRXUAVKZWYOVKZVKZXUEWYP
      WYGXUGXUDWYQWYGXUGXUFWYSXUBVKZVKZXUDWYGXUGXUJWYGXUFWYOXUIWVTXUFWXNWYOXUIX
      FZWVTXUFVKZWWNXUKWWOXULWYLWQVCZWYJWQVCZVKZWWNXUKWUKWYRWURXUAXUOWUKWYRVKXU
      MWURXUAVKXUNVWLWUKWYRXUMQVYPWYKEGOYTXTVWNWURXUAXUNRVYQWYIFHPYTXTUUAYBXUMX
      UNWWNXUKXUMXUNWWNUUBZWYOWYLWYJXKWPZWYJWYLXLZWWMWOWPXUIXUPWYNXURWWMWOXUPWY
      NXUQUUCZWYLWYJXLXURXUPWYMXUSWYLWYJXUMXUNWYMXUSXFZWWNXUNXUMXUTWYJWYLUUDUUE
      UUFUUGXUQWYLWYJUUHUUIUUJWYLWYJWWMUUKUULUUMXEUUNUUOUUPUUQWYRXUAWYSXUBYDUUR
      UUSWYPWYQXUFVKZWYOVKXUHWYHXVAWYOVVQGHUUTUVAWYQXUFWYOUVBUVCUVDWYGWYDWYHVYR
      VVQDVAZWWMWOWPZVKZWYPWVTVYRVXIVCZWXNWYDXVDXFZVYPVYQGHUVEZDVDVCZXVEWXNXVFI
      JKEFDGHOPQRSUVFZVVQDVYRWWMVXIDUVGZUVGVXIVXIUKZUVGVXIXVJXVKIJKVXIVXIIUJZYS
      UPJUJZYSUPEVAXVLYRUPXVMYRUPFVAUVHZWQWOUVIDWQXVNWOUVJUVKSUVLUVMVXIUVNUVOZU
      VPUVQXEWYGWYHXVCWYOWVTWYHXVCWYOXFZVLZWXNWVTXVEXVQXVGXVEWYHXVPXVEWYHVKXVBW
      YNWWMWOIJKEFDVYRVVQVYPVYQWYKWYIGHOPQRSVYRYSUPZVYPVYPVYQUGWEZUVRZVRZVYRYRU
      PZVYQVYPVYQXVSUHWEUVSZVRZWYKVPWYIVPUVTUUJUWAYNYGUWBUULWYGWYQWYKWXHVCZWYIW
      XJVCZVKZVKXUEWYEWYGXWGXUDWYQWYGXWEWYTXWFXUCWUKWXNXWEWYTXFZWURVWLWUKWXNXWH
      QWYKEVYPWWMGOUVPUVQUWCWURWXNXWFXUCXFZWUKVWNWURWXNXWIRWYIFVYQWWMHPUVPUVQYH
      UWDWKVVQWXHWXJUUTUWEUWFUWGUWHUWIWWPWYBVVTYMYNWXHVVRWXJVVSUWJUWKUWLUWMUWNY
      BWUGWWOWWQVKUFWWMWQVYJWWMULZVYKWWOWUFWWQVYJWWMWNWOUWOXWJWUEWWPVVTVYJWWMVY
      RVWEUWPXAUWDUWQUWRYBUWAYBUWAUWSWUPWVCUCUDWQWQUWTUXAUXBYBYCUXCYBUWAUXDUXEY
      OUXFUGUHVWRVVRVVSUXGUVDUXBVXJVYHVYOXFZVYDVYEXVHVXJXWKXVIUEUFVVTDVWRVXIXVO
      UXHXCYPUXIUWAUXCUWAUWSUXBUWNUXJVXSVWFVTVCZVXGVWFVNZULVWQVXHXFVXTXVHXWLXVI
      DVWHVWHVPZUXKVFZXWMVXIVXNVXGXVHXWMVXIULXVIDVXIXVOUXLVFVXMVXNVXIVXOVXRUXMV
      YAUXNUCUDUEVWCVWFUXOUXPUXQXWLVXSVWFVWCVJVWGVWDVJXWOVXTUEVWFVWCVXAVWFVCZVY
      KVXAVWRVWSDVAZVYJWOWPZUDVXIUXRZULZVKZUFWQUMUCVXIUMZVXAVWCVCZXVHXWPXXBXFXV
      IUCUFUDVXADVXIXVOUXSVFXXAXXCUCUFVXIWQVXJVYJWQVCZVKZVYKXWTXXCXWTXXCXWSVWCV
      CZXXEVYKVKZVXAXWSVWCUXTXXGXWSVVTULZUBBUMUAAUMZXXFXXGVWRYSUPZVYJWUMVAZAVCZ
      VWRYRUPZVYJWUTVAZBVCZXWSXXKXXNUKZULZXXIVXJXXDVYKXXLVXJXXJGVCZXXDVYKVKZXXL
      VWRGHUYAVWLXXRXXSXXLQEXXJVYJAGOLUYBUVQXEUWHVXJXXDVYKXXOVXJXXMHVCZXXSXXOVW
      RGHUYCVWNXXTXXSXXORFXXMVYJBHPMUYBUVQXEUWHXXGVVRVVSWRZXWSVCZXYAXXPVCZXFZUB
      UYDUAUYDZXXQXXGXYDUAUBXXGVVRGVCZVVSHVCZVKZVWRXYADVAZVYJWOWPZVKZVVRXXKVCZV
      VSXXNVCZVKZXYBXYCVXJXXDVYKXYKXYNXFZVXJXXSXYOVXJVYSWVTVKZUHWSUGWSXXSXYOVLZ
      UGUHVWRGHUXGXYPXYQUGUHVYSWVTXXSXYOVYSXYOXYHVYRXYADVAZVYJWOWPZVKZVVRVYPVYJ
      WUMVAZVCZVVSVYQVYJWUTVAZVCZVKZXFWVTXXSVKZVYSXYKXYTXYNYUEVYSXYJXYSXYHVYSXY
      IXYRVYJWOVWRVYRXYADWTUUJWKVYSXYLYUBXYMYUDVYSXXKYUAVVRVYSXXJVYPVYJWUMVYSXX
      JXVRVYPVWRVYRYSUYEXVTUUIUYFUYGVYSXXNYUCVVSVYSXXMVYQVYJWUTVYSXXMXWBVYQVWRV
      YRYRUYEXWCUUIUYFUYGUWDUYHYUFXYTXYHYUEVKYUEYUFXYHXYSYUEXXSWVTXYHXYSYUEXFZV
      LXXSWVTXYHYUGWVTXYHVKZXXSYUGYUHXXSVKZXYSVYQVVSFVAZVYPVVREVAZWOWPZYUKYUJXL
      ZVYJWOWPYUKYUJXKWPZYUJYUKXLZVYJWOWPZYUEYUIXYRYUMVYJWOYUHXYRYUMULZXXSWVTXV
      EXYAVXIVCZYUQXYHXVGVVRVVSGHUVEIJKEFDVYRXYAVYPVYQVVRVVSGHOPQRSXWAXWDXYAYSU
      PVVRVVRVVSUAWEZUVRVRXYAYRUPVVSVVRVVSYUSUBWEZUVSVRUVTYCYGUUJYUIYUMYUOVYJWO
      YUHYUMYUOULXXSYUHYUMYUNUUCZYUKYUJXLYUOYUHYULYVAYUKYUJWUKXYFWURXYGYULYVAXF
      ZWURXYGVKZWUKXYFVKZYVBYVCYUJWQVCZYUKWQVCZYVBYVDVWNWURXYGYVERVYQVVSFHPYTXT
      ZVWLWUKXYFYVFQVYPVVREGOYTXTZYUJYUKUUDYCUUEYBUUGYUNYUKYUJUUHUUIYGUUJYUIYUP
      YUKVYJWOWPZYUJVYJWOWPZVKZYUEYUHYVFYVEVKZXXSYUPYVKXFZWUKXYFWURXYGYVLYVDYVF
      YVCYVEYVHYVGUUAYBYVLXXDYVMVYKYVFYVEXXDYVMYUKYUJVYJUUKUUMUUNXEYUHXXSYVKYUE
      XFZWUKXYFWURXYGXXSYVNVLXXSYVDYVCVKZYVNXXSYVOYVNVLYVOXXSXXSVKYVNYVDXXSYVCX
      XSYVNYVDXXSVKZYVIYUBYVCXXSVKZYVJYUDYVPYUBYVIVWLWUKXYFXXSYUBYVIXFQVVREVYPV
      YJGOUYIUYJUYKYVQYUDYVJVWNWURXYGXXSYUDYVJXFRVVSFVYQVYJHPUYIUYJUYKUYLYBUYMU
      YNUYOYBUXBUULUYPUYMUYQUYRUWBYUFYUEXYHYUFYUBXYFYUDXYGYUFYUAGVVRWUKXXSYUAGV
      JZWURVWLWUKXXSYVRQEVYPVYJGOUYSUVQUWCUYTYUFYUCHVVSWURXXSYUCHVJZWUKVWNWURXX
      SYVSRFVYQVYJHPUYSUVQYHUYTVUAVUBVUCVUDVUEVUFVUGUXBUWHXYBYURXYJVKXYKXWRXYJU
      DXYAVXIVWSXYAULXWQXYIVYJWOVWSXYAVWRDUWPUUJVUHYURXYHXYJVVRVVSGHYUTVUIUVAUV
      CVVRVVSXXKXXNYUTVUIVUJVUKXWSVULZXXPVULXXQXYEXFXWSVXIVJVXIVULYVTXWRUDVXIVU
      MGHVUNXWSVXIVUOVUPXXKXXNVUNUAUBXWSXXPVUQVHVURXXHXXQXWSXXKVVSUKZULUAUBXXKX
      XNABVVRXXKULVVTYWAXWSVVRXXKVVSVUSVUTVVSXXNULYWAXXPXWSVVSXXNXXKVVAVUTVVBVV
      CVWBXXITXWSXWRUDVXIGHGEUVGZUVGVVDOYWBEEVDQVVEVVFVVFVVGHFUVGZUVGVVDPYWCFFV
      DRVVEVVFVVFVVGVVHVVIVVQXWSULVWAXXHUAUBABVVQXWSVVTWFWGWHVURUXCVVJVVKVUGVVL
      VWFVWCVVMUXPVVNXVHVWGVWHULXVIDVWHXWNVVOVFVVP $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d C x y z $.  $d J x y z $.
    txcc.1 $e |- C = ( abs o. - ) $.
    txcc.2 $e |- J = ( MetOpen ` C ) $.
    $( The product of two copies of ` CC ` , in terms of the sup norm. $)
    txcc $p |- ( J tX J ) = ( MetOpen ` { <. <. x , y >. , z >. | ( ( x e.
          ( CC X. CC ) /\ y e. ( CC X. CC ) ) /\ z = sup ( { ( ( 1st ` x ) C
            ( 1st ` y ) ) , ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) } ) $=
      ( ctx co cv cc cxp wcel wa c1st cfv c2nd cpr cr clt csup wceq copab2
      eqid cnmetba cnmet txmet ) EEEEHIZAJZKKLZMBJZUJMNCJUIOPUKOPDIUIQPUKQPDIRS
      TUAUBNABCUCZDDKKABCGGUHUDDFUEZUMDFUFZUNULUDUG $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d C x y z $.  $d J x y z $.
    addcntx.1 $e |- C = ( abs o. - ) $.
    addcntx.2 $e |- J = ( MetOpen ` C ) $.
    $( Continuity of addition, written using topological products. $)
    addcntx $p |- + e. ( ( J tX J ) Cn J ) $=
      ( vx vy vz cv cc cxp wcel wa c1st cfv co c2nd cpr cr clt csup wceq
      copab2 ctx eqid txcc addcn ) EFGAEHZIIJZKFHZUHKLGHUGMNUIMNAOUGPNUIPNAOQRS
      TUALEFGUBZBBBUCOCUJUDDEFGABCDUEUF $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d C x y z $.  $d J x y z $.
    subcntx.1 $e |- C = ( abs o. - ) $.
    subcntx.2 $e |- J = ( MetOpen ` C ) $.
    $( Continuity of subtraction, written using topological products. $)
    subcntx $p |- - e. ( ( J tX J ) Cn J ) $=
      ( vx vy vz cv cc cxp wcel wa c1st cfv co c2nd cpr cr clt csup wceq
      copab2 ctx eqid txcc subcn ) EFGAEHZIIJZKFHZUHKLGHUGMNUIMNAOUGPNUIPNAOQRS
      TUALEFGUBZBBBUCOCUJUDDEFGABCDUEUF $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d C x y z $.  $d J x y z $.
    mulcntx.1 $e |- C = ( abs o. - ) $.
    mulcntx.2 $e |- J = ( MetOpen ` C ) $.
    $( Continuity of multiplication, written using topological products. $)
    mulcntx $p |- x. e. ( ( J tX J ) Cn J ) $=
      ( vx vy vz cv cc cxp wcel wa c1st cfv co c2nd cpr cr clt csup wceq
      copab2 ctx eqid txcc mulcn ) EFGAEHZIIJZKFHZUHKLGHUGMNUIMNAOUGPNUIPNAOQRS
      TUALEFGUBZBBBUCOCUJUDDEFGABCDUEUF $.
      $( [11-Jun-2010] $)
  $}

