$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Product topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    txtopiOLD.1 $e |- R e. Top $.
    txtopiOLD.2 $e |- S e. Top $.
    $( The product of two topologies is a topology.  (Moved to ~ txtopi in main
       set.mm and may be deleted by mathbox owner, JM. --NM 15-Oct-2012.) $)
    txtopiOLD $p |- ( R tX S ) e. Top $=
      ( txtopi ) ABCDE $.
      $( [15-Jun-2010] $)
  $}

  ${
    txuniiOLD.1 $e |- R e. Top $.
    txuniiOLD.2 $e |- S e. Top $.
    txuniiOLD.3 $e |- X = U. R $.
    txuniiOLD.4 $e |- Y = U. S $.
    $( The underlying set of the product of two topologies.  (Moved to ~ txunii
       in main set.mm and may be deleted by mathbox owner, JM. --NM
       15-Oct-2012.) $)
    txuniiOLD $p |- ( X X. Y ) = U. ( R tX S ) $=
      ( txunii ) ABCDEFGHI $.
      $( [15-Jun-2010] $)
  $}

  ${
    $d T x y z w $.  $d R x y z w $.  $d S x y z w $.  $d U x y z w $.
    $d X x y z $.  $d Y x y z $.  $d F x y z w $.  $d G x y z w $.  $d H w $.
    $d Z x y z $.  $d W x y z w $.
    txcnoprab.1 $e |- T = ( R tX S ) $.
    txcnoprab.2 $e |- X = U. U $.
    txcnoprab.3 $e |- Y = U. W $.
    txcnoprab.4 $e |- Z = ( U tX W ) $.
    txcnoprab.5 $e |- H = { <. <. x , y >. , z >. | ( ( x e. X /\ y e. Y ) /\
                                      z = <. ( x F y ) , ( x G y ) >. ) } $.
    $( A map into the product of two topological spaces is continuous if both
       of its projections are continuous.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    txcnoprab $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( U e. Top /\ W e. Top ) /\
              ( F e. ( Z Cn R ) /\ G e. ( Z Cn S ) ) ) -> H e. ( Z Cn T ) ) $=
      ( wcel vw ctop wa ccn co wi c0 csn cif ctx cv cuni cop wceq copab2 eleq2i
      opreq1i anbi12i anbi2i anbi1i eqtri eleq12i imbi12i opreq1 opreq1d eleq2d
      oprabbii anbi12d anbi2d unieq anbi1d eleq12d imbi12d syl5bb opreq2 sn0top
      oprabbidv elimel eqid txtop mp2an cxp txuni eqcomi df-opr opeq12i opeq12d
      cfv fveq2 syl5eq dfoprab4s txcnopab mp3anl3 dedth2h imp an12s 3impb ) DUB
      TZEUBTZUCZGUBTZKUBTZUCZHNDUDUEZTZINEUDUEZTZUCZJNFUDUEZTZXCWTXHXJXCWTXHUCZ
      XJXAXBXKXJUFZWTHXAGUGUHZUIZKUJUEZDUDUEZTZIXOEUDUEZTZUCZUCZAUKZXNULZTZBUKZ
      KULZTZUCZCUKYBYEHUEZYBYEIUEZUMZUNZUCZABCUOZXOFUDUEZTZUFZWTHXNXBKXMUIZUJUE
      ZDUDUEZTZIYSEUDUEZTZUCZUCZYDYEYRULZTZUCZYLUCZABCUOZYSFUDUEZTZUFGKXMXMXLWT
      HGKUJUEZDUDUEZTZIUUMEUDUEZTZUCZUCZYBGULZTZYGUCZYLUCZABCUOZUUMFUDUEZTZUFGX
      NUNZYQXKUUSXJUVFXHUURWTXEUUOXGUUQXDUUNHNUUMDUDRUQUPXFUUPINUUMEUDRUQUPURUS
      JUVDXIUVEJYBLTZYEMTZUCZYLUCZABCUOUVDSUVKUVCABCUVJUVBYLUVHUVAUVIYGLUUTYBPU
      PMYFYEQUPURUTVGVANUUMFUDRUQVBVCUVGUUSYAUVFYPUVGUURXTWTUVGUUOXQUUQXSUVGUUN
      XPHUVGUUMXODUDGXNKUJVDZVEVFUVGUUPXRIUVGUUMXOEUDUVLVEVFVHVIUVGUVDYNUVEYOUV
      GUVCYMABCUVGUVBYHYLUVGUVAYDYGUVGUUTYCYBGXNVJVFVKVKVQUVGUUMXOFUDUVLVEVLVMV
      NKYRUNZYAUUEYPUULUVMXTUUDWTUVMXQUUAXSUUCUVMXPYTHUVMXOYSDUDKYRXNUJVOZVEVFU
      VMXRUUBIUVMXOYSEUDUVNVEVFVHVIUVMYNUUJYOUUKUVMYMUUIABCUVMYHUUHYLUVMYGUUGYD
      UVMYFUUFYEKYRVJVFVIVKVQUVMXOYSFUDUVNVEVLVMWRWSYSUBTZUUDUULXNUBTZYRUBTZUVO
      GXMUBVPVRZKXMUBVPVRZXNYRYSYSVSZVTWAUACDEFYSHIUUJYCUUFWBZOYSULZUWAUVPUVQUW
      BUWAUNUVRUVSXNYRYSYCUUFUVTYCVSUUFVSWCWAWDABCUAYCUUFYKUAUKZHWHZUWCIWHZUMZY
      BYEUMZUWCUNZYKUWGHWHZUWGIWHZUMUWFYIUWIYJUWJYBYEHWEYBYEIWEWFUWHUWIUWDUWJUW
      EUWGUWCHWIUWGUWCIWIWGWJWKWLWMWNWOWPWQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R r s u v w x y z $.  $d S r s u v w x y z $.  $d V r s u v w x y z $.
    $d W r s u v w x y z $.  $d A r s u v w x y z $.  $d B r s u v w x y z $.
    $( The subspace of a topological product space induced by a subset with a
       Cartesian product representation is a topological product of the
       subspaces induced by the subspaces of the terms of the products.
       (Contributed by Jeff Madsen, 2-Sep-2009.)  (The proof was shortened by
       Mario Carneiro, 15-Dec-2013.) $)
    txsubsp $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. V /\ B e. W ) ) ->
                      ( subSp ` <. ( A X. B ) , ( R tX S ) >. ) =
                    ( ( subSp ` <. A , R >. ) tX ( subSp ` <. B , S >. ) ) ) $=
      ( vx vz vu vv vy vr vs ctop wcel wa cv wceq wrex syl5ibrcom vw cxp ctx co
      cop csubsp cfv cab wss cuni wex cin cvv wb eqid txtop xpexg elsubsp eltpt
      syl2an adantr ssel2 visset eqeq1 2rexbidv sylib wi w3a an6 elsubspr xpeq1
      eqeq2d xpeq2 rcla42ev mp3an3 sylbir ineq1 syl6eq eqeq1d 3expia r19.23advv
      elab inxp imp sylan2 anassrs r19.23adva ss2abdv inuni abrexex sseq1 unieq
      anbi12d cla4ev sylancl anbi2d exbidv 19.23adv sylbid stoig3 bi2anan9 an4s
      expimpd reeanv syl6bbr ad2antrr ad2antlr adantlr syl111anc xpeq12 syl6eqr
      txopn eleq1d eleq1a syl6 abssdv ancoms sylan uniopn syl11anc eleq1 impbid
      ex sstr bitr4d eqrdv ) CNOZDNOZPZAEOZBFOZPZPZGABUBZCDUCUDZUEUFUGZACUEUFUG
      ZBDUEUFUGZUCUDZYMGQZYPOZHQZYTIQZJQZUBZRZJYRSIYQSZGUHZUIZYTUUBUJZRZPZHUKZY
      TYSOZYMUUAUUMYMUUAYTKQZYNULZRZKYOSZUUMYIYONOZYNUMOZUUAUURUNYLCDYOYOUOZUPZ
      ABEFUQZKYTYNYOUMURUTYMUUQUUMKYOYMUUOYOOZPUUMUUQUUIUUPUUJRZPZHUKZYMUVDUVGY
      MUVDUAQZUUBLQZMQZUBZRZMDSLCSZHUHZUIZUUOUVHUJZRZPZUAUKZUVGYIUVDUVSUNYLUAHM
      LUUOUVNCDUVNUOUSVAYMUVRUVGUAYMUVOUVQUVGYMUVOPZUVGUVQUUIUVPYNULZUUJRZPZHUK
      ZUVTUUQKUVHSZGUHZUUHUIZUWAUWFUJZRZUWDUVTUWEUUGGUVTUUQUUGKUVHUVTUUOUVHOZPU
      UGUUQUUPUUERZJYRSIYQSZYMUVOUWJUWLUVOUWJPZYMUUOUVKRZMDSLCSZUWLUWMUUOUVNOUW
      OUVHUVNUUOVBUVMUWOHUUOKVCUUBUUORUVLUWNLMCDUUBUUOUVKVDVEWBVFYMUWOUWLYMUWNU
      WLLMCDYIYLUVICOZUVJDOZPZUWNUWLVGYIYLUWRVHZUWLUWNUVIAULZUVJBULZUBZUUERZJYR
      SIYQSZUWSYGYJUWPVHZYHYKUWQVHZPUXDYGYJUWPYHYKUWQVIUXEUWTYQOZUXAYROZUXDUXFU
      VIACEVJUVJBDFVJUXGUXHUXBUXBRZUXDUXBUOUXCUXIUXBUWTUUDUBZRIJUWTUXAYQYRUUCUW
      TRZUUEUXJUXBUUCUWTUUDVKVLUUDUXARZUXJUXBUXBUUDUXAUWTVMVLVNVOUTVPUWNUWKUXCI
      JYQYRUWNUUPUXBUUEUWNUUPUVKYNULZUXBUUOUVKYNVQUVIUVJABWCZVRVSVETVTWAWDWEWFU
      UQUUFUWKIJYQYRYTUUPUUEVDVETWGWHGKUVHYNWIUWCUWGUWIPHUWFKGUVHUUPUAVCWJUUBUW
      FRZUUIUWGUWBUWIUUBUWFUUHWKUXOUUJUWHUWAUUBUWFWLVLWMWNWOUVQUVFUWCHUVQUVEUWB
      UUIUVQUUPUWAUUJUUOUVPYNVQVSWPWQTXCWRWSWDUUQUULUVFHUUQUUKUVEUUIYTUUPUUJVDW
      PWQTWGWSYMUULUUAHYMUUIUUKUUAYMUUIPZUUAUUKUUJYPOZUXPYPNOZUUBYPUIZUXQYMUXRU
      UIYIUUSUUTUXRYLUVBUVCYNYOUMWTUTVAYMUUHYPUIZUUIUXSYMUUGGYPYMUUFUUAIJYQYRYM
      UUCYQOZUUDYROZPZUUEYPOZUUFUUAVGYMUYCUXKUXLPZMDSLCSZUYDYMUYCUXKLCSZUXLMDSZ
      PZUYFYGYJYHYKUYCUYIUNYGYJPZUYAUYGYHYKPZUYBUYHLUUCACEURMUUDBDFURXAXBUXKUXL
      LMCDXDXEYMUYEUYDLMCDYMUWRUYEUYDVGYMUWRPZUYDUYEUXMYPOZUYLUUSUUTUVKYOOZUYMY
      IUUSYLUWRUVBXFYLUUTYIUWRUVCXGYIUWRUYNYLUVIUVJCDYOUVAXLXHUVKYNYOUMVJXIUYEU
      UEUXMYPUYEUUEUXBUXMUUCUWTUUDUXAXJUXNXKXMTYCWAWSUUEYPYTXNXOWAXPUUIUXTUXSUU
      BUUHYPYDXQXRUUBYPXSXTYTUUJYPYATXCWRYBYGYJYHYKUUNUUMUNZUYJYQNOYRNOUYOUYKAC
      EWTBDFWTHGJIYTUUHYQYRUUHUOUSUTXBYEYF $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d R u v z $.  $d S u v z $.  $d T u v z $.  $d A u v z $.  $d B u v z $.
    txopnOLD.1 $e |- T = ( R tX S ) $.
    $( The product of two open sets is open in the product topology.  (Moved to
       ~ txopn in main set.mm and may be deleted by mathbox owner, JM. --NM
       15-Oct-2012.) $)
    txopnOLD $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. R /\ B e. S ) )
                                                      -> ( A X. B ) e. T ) $=
      ( txopn ) ABCDEFG $.
      $( [2-Sep-2009] $)
  $}

  ${
    txcldOLD.1 $e |- T = ( R tX S ) $.
    $( The product of two closed sets is closed in the product topology.
       (Moved to ~ txcld in main set.mm and may be deleted by mathbox owner,
       JM. --NM 15-Oct-2012.) $)
    txcldOLD $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. ( Clsd ` R )
                  /\ B e. ( Clsd ` S ) ) ) -> ( A X. B ) e. ( Clsd ` T ) ) $=
      ( txcld ) ABCDEFG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A w x y z $.  $d B w x y z $.  $d C u v w x y z $.  $d D u v w x y z $.
    $d S w $.  $d T u v w x y $.  $d J w $.  $d K w $.  $d L w $.
    $d F u v w $.  $d R w z $.
    cnresoprab.1 $e |- A = U. J $.
    cnresoprab.2 $e |- B = U. K $.
    cnresoprab.3 $e |- S = U. L $.
    cnresoprab.4 $e |- C C_ A $.
    cnresoprab.5 $e |- D C_ B $.
    cnresoprab.6 $e |- T C_ S $.
    cnresoprab.7 $e |- ( ( x e. C /\ y e. D ) -> R e. T ) $.
    cnresoprab.8 $e |- F = { <. <. x , y >. , z >. |
                                        ( ( x e. A /\ y e. B ) /\ z = R ) } $.
    cnresoprab.9 $e |- G = { <. <. x , y >. , z >. |
                                        ( ( x e. C /\ y e. D ) /\ z = R ) } $.
    cnresoprab.10 $e |- J e. Top $.
    cnresoprab.11 $e |- K e. Top $.
    cnresoprab.12 $e |- L e. Top $.
    cnresoprab.13 $e |- F e. ( ( J tX K ) Cn L ) $.
    $( Continuity of a restricted operation abstraction.  (Contributed by Jeff
       Madsen, 11-Jun-2010.) $)
    cnresoprab $p |- G e. ( ( subSp ` <. ( C X. D ) , ( J tX K ) >. )
                                                Cn ( subSp ` <. T , L >. ) ) $=
      ( vw vu vv cxp cres ctx co cop csubsp cfv ccn cv wcel wa copab2 resoprab2
      wceq wss mp2an reseq1 ax-mp 3eqtr4ri ctop wral txtop pm3.2i xpss12 csb wi
      eqid ax-17 cvv wal visset hbcsb1 hbcsbg hbel hbim anbi1d csbeq1a csbeq2dv
      eleq1 mpan2 eleq1d imbi12d anbi2d chvar oprabval2gf 3expia syl2an eqeltrd
      sseli mpd rgen2 fveq2 df-opr syl6eqr ralxp mpbir cuni txuni eqcomi cnres2
      mp3an eqeltri ) LKFGULZUMZXNMNUNUOZUPUQURJOUPUQURUSUOZAUTZDVABUTZEVAVBCUT
      HVEZVBABCVCZXNUMZXRFVAZXSGVAZVBZXTVBABCVCZXOLFDVFZGEVFZYBYFVESTXTABCDEFGV
      DVGKYAVEXOYBVEUCKYAXNVHVIUDVJXPVKVAZOVKVAZVBXNDEULZVFZJIVFZVBKXPOUSUOVAZU
      IUTZKURZJVAZUIXNVLZVBXOXQVAYIYJMVKVAZNVKVAZYIUEUFMNXPXPVRZVMVGUGVNYLYMYGY
      HYLSTFDGEVOVGUAVNYNYRUHYRUJUTZUKUTZKUOZJVAZUKGVLUJFVLUUEUJUKFGUUBFVAZUUCG
      VAZVBZUUDBUUCAUUBHVPZVPZJUUHUUJJVAZUUDUUJVEZYCUUGVBZBUUCHVPZJVAZVQZUUHUUK
      VQAUJUUHUUKAUUHAVSAUIUIUUJJUUCVTVAZYOUUJVAZUURAWAVQUKWBZABUIUUCUUIVTYOUUC
      VAZAVSAUIUUBHUJWBYOUUBVAZAVSZWCZWDVIYOJVAZAVSWEWFXRUUBVEZUUMUUHUUOUUKUVEY
      CUUFUUGXRUUBFWJWGUVEUUNUUJJUVEUUQUUNUUJVEUUSUVEBUUCHUUIVTAUUBHWHZWIWKWLWM
      YEHJVAZVQUUPBUKUUMUUOBUUMBVSBUIUIUUNJBUIUUCHUUSUUTBVSZWCUVDBVSWEWFXSUUCVE
      ZYEUUMUVGUUOUVIYDUUGYCXSUUCGWJWNUVIHUUNJBUUCHWHWLWMUBWOWOZUUFUUBDVAZUUCEV
      AZUUKUULVQUUGFDUUBSWTGEUUCTWTUVKUVLUUKUULABCUIUUBUUCDEHUUJKUUIJUVBUVABVSU
      VACVSUVHUUTCVSUVCBUIUUCUUIUUSUVHWCUVFBUUCUUIWHUCWPWQWRXAUVJWSXBYQUUEUIUJU
      KFGYOUUBUUCUPZVEZYPUUDJUVNYPUVMKURUUDYOUVMKXCUUBUUCKXDXEWLXFXGVNUIXNJKXPO
      YKIXPXHZYKYSYTUVOYKVEUEUFMNXPDEUUAPQXIVGXJRXKXLXM $.
      $( [19-Dec-2013] $) $( [11-Jun-2010] $)

    cnresoprab.14 $e |- M = ( subSp ` <. C , J >. ) $.
    cnresoprab.15 $e |- N = ( subSp ` <. D , K >. ) $.
    cnresoprab.16 $e |- O = ( subSp ` <. T , L >. ) $.
    $( Continuity of a restricted operation abstraction.  (Contributed by Jeff
       Madsen, 15-Jun-2010.) $)
    cnresoprab2 $p |- G e. ( ( M tX N ) Cn O ) $=
      ( cxp ctx co cop csubsp cfv ccn cnresoprab opreq12i ctop wcel wceq uniexg
      cvv cuni ax-mp eqeltri ssexi txsubsp mp4an eqtr4i eleqtrri ) LFGUOMNUPUQU
      RUSUTZJOURUSUTZVAUQPQUPUQZRVAUQABCDEFGHIJKLMNOSTUAUBUCUDUEUFUGUHUIUJUKVBV
      SVQRVRVAVSFMURUSUTZGNURUSUTZUPUQZVQPVTQWAUPULUMVCMVDVEZNVDVEZFVHVEGVHVEVQ
      WBVFUHUIFDDMVIZVHSWCWEVHVEUHMVDVGVJVKUBVLGEENVIZVHTWDWFVHVEUINVDVGVJVKUCV
      LFGMNVHVHVMVNVOUNVCVP $.
      $( [15-Dec-2013] $) $( [15-Jun-2010] $)
  $}

  ${
    $d A v x z $.  $d A x y $.  $d A w $.  $d J v w $.  $d K v x w $.
    $d L v x w u $.  $d F u v w $.  $d G u v w $.  $d R y $.  $d R w $.
    $d S z $.  $d S w $.  $d P x w $.
    cnopropabco.1 $e |- A = U. J $.
    cnopropabco.2 $e |- J e. Top $.
    cnopropabco.3 $e |- K e. Top $.
    cnopropabco.4 $e |- L e. Top $.
    cnopropabco.5 $e |- M e. Top $.
    cnopropabco.6 $e |- ( x e. A -> R e. X ) $.
    cnopropabco.7 $e |- ( x e. A -> S e. Y ) $.
    cnopropabco.8 $e |- F = { <. x , y >. | ( x e. A /\ y = R ) } $.
    cnopropabco.9 $e |- G = { <. x , z >. | ( x e. A /\ z = S ) } $.
    cnopropabco.10 $e |- H = { <. x , w >. | ( x e. A /\ w = ( R P S ) ) } $.
    cnopropabco.11 $e |- F e. ( J Cn K ) $.
    cnopropabco.12 $e |- G e. ( J Cn L ) $.
    cnopropabco.13 $e |- P e. ( ( K tX L ) Cn M ) $.
    $( The composition of a continuous operation with two continuous functions
       is continuous.  (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    cnopropabco $p |- H e. ( J Cn M ) $=
      ( vv vu cv wcel cfv cop wceq wa copab ccom ccn cuni cxp wfn ctx ctop eqid
      co wf txtop mp2an txuni eqcomi cnf mp3an ffn ax-mp fvopab2 mpdan ffvelrni
      fveq1i syl5eq eqeltrrd opeq12d eqeq2d pm5.32i opabbii opropabco w3a ax-17
      3pm3.2i hbopab1 hbxfreq hbfv hbop hbeq hban eleq1 anbi12d cbvopab1 pm3.2i
      fveq2 txcnopab eqeltri cnco ) KFAUMZEUNZDUMZXFIUOZXFJUOZUPZUQZURZADUSZUTZ
      LOVAVHZFMVBZNVBZVCZVDZKXOUQXSOVBZFVIZXTMNVEVHZVFUNZOVFUNZFYCOVAVHUNZYBMVF
      UNZNVFUNZYDTUAMNYCYCVGZVJVKZUBUJFYCOXSYAYCVBZXSYGYHYKXSUQTUAMNYCXQXRYIXQV
      GZXRVGZVLVKVMYAVGVNVOXSYAFVPVQADEGHXQXRXNKFXGXIGXQXGXIXFXGBUMGUQURZABUSZU
      OZGXFIYOUEWAXGGPUNYPGUQUCABEGPVRVSWBZEXQXFILVFUNZYGILMVAVHUNZEXQIVISTUHIL
      MEXQRYLVNVOVTWCXGXJHXRXGXJXFXGCUMHUQURZACUSZUOZHXFJUUAUFWAXGHQUNUUBHUQUDA
      CEHQVRVSWBZEXRXFJYRYHJLNVAVHUNZEXRJVISUAUIJLNEXRRYMVNVOVTWCXMXGXHGHUPZUQZ
      URADXGXLUUFXGXKUUEXHXGXIGXJHYQUUCWDWEWFWGUGWHVQYRYDYEWIXNLYCVAVHZUNZYFURX
      OXPUNYRYDYESYJUBWKUUHYFXNUKUMZEUNZXHUUIIUOZUUIJUOZUPZUQZURZUKDUSZUUGXMUUO
      ADUKXMUKWJUUJUUNAUUJAWJAULULXHUUMULUMZXHUNAWJAULUUKUULAULUUIIAULIYOUEYNAB
      ULWLWMUUQUUIUNAWJZWNAULUUIJAULJUUAUFYTACULWLWMUURWNWOWPWQXFUUIUQZXGUUJXLU
      UNXFUUIEWRUUSXKUUMXHUUSXIUUKXJUULXFUUIIXBXFUUIJXBWDWEWSWTYGYHYRWIYSUUDURU
      UPUUGUNYGYHYRTUASWKYSUUDUHUIXAUKDMNYCLIJUUPEYIRUUPVGXCVKXDUJXAXNFLYCOXEVK
      XD $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d J x w $.  $d F w $.  $d G w $.  $d R y w $.  $d S z w $.  $d P x w $.
    $d x y z w $.
    cnopropabcoc.1 $e |- J = ( MetOpen ` ( abs o. - ) ) $.
    cnopropabcoc.2 $e |- R e. _V $.
    cnopropabcoc.3 $e |- S e. _V $.
    cnopropabcoc.4 $e |- F = { <. x , y >. | ( x e. CC /\ y = R ) } $.
    cnopropabcoc.5 $e |- G = { <. x , z >. | ( x e. CC /\ z = S ) } $.
    cnopropabcoc.6 $e |- H = { <. x , w >. | ( x e. CC /\ w = ( R P S ) ) } $.
    cnopropabcoc.7 $e |- F e. ( CC -cn-> CC ) $.
    cnopropabcoc.8 $e |- G e. ( CC -cn-> CC ) $.
    cnopropabcoc.9 $e |- P e. ( ( J tX J ) Cn J ) $.
    $( The composition of a continuous operation with two continuous functions
       is continuous.  Version for functions ` CC --> CC ` .  (Contributed by
       Jeff Madsen, 11-Jun-2010.) $)
    cnopropabcoc $p |- H e. ( CC -cn-> CC ) $=
      ( ccn co ccncf cvv cuni cabs cmin ccom cme wcel wceq eqid cnmetba uniopn2
      cc cnmet ax-mp eqcomi opntop cv a1i cncfmet1 eleqtri cnopropabco eleqtrri
      ctop ) JKKUAUBZUOUOUCUBZABCDUOEFGHIJKKKKUDUDKUEZUOUFUGUHZUIUJZVIUOUKVJVJU
      LZUPZVJKUOVJVLUMLUNUQURVKKVFUJVMVJKLUSUQZVNVNVNFUDUJAUTUOUJZMVAGUDUJVONVA
      OPQHVHVGRVJKVLLVBZVCIVHVGSVPVCTVDVPVE $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d J s t w x y $.  $d K s t w x y $.  $d L s t w x y z $.
    $d M s t w x y z $.  $d A s t u v w x y z $.  $d B s t u v w x y z $.
    $d R u w z $.  $d S v w z $.  $d F r s t w z $.  $d G r s t w z $.
    $d H w $.  $d P w x y z $.  $d r x y $.
    cnoproprabco.1 $e |- A = U. J $.
    cnoproprabco.2 $e |- B = U. K $.
    cnoproprabco.3 $e |- J e. Top $.
    cnoproprabco.4 $e |- K e. Top $.
    cnoproprabco.5 $e |- L e. Top $.
    cnoproprabco.6 $e |- M e. Top $.
    cnoproprabco.7 $e |- N e. Top $.
    cnoproprabco.8 $e |- ( ( x e. A /\ y e. B ) -> R e. X ) $.
    cnoproprabco.9 $e |- ( ( x e. A /\ y e. B ) -> S e. Y ) $.
    cnoproprabco.10 $e |- F = { <. <. x , y >. , u >. |
                                        ( ( x e. A /\ y e. B ) /\ u = R ) } $.
    cnoproprabco.11 $e |- G = { <. <. x , y >. , v >. |
                                        ( ( x e. A /\ y e. B ) /\ v = S ) } $.
    cnoproprabco.12 $e |- H = { <. <. x , y >. , z >. |
                                ( ( x e. A /\ y e. B ) /\ z = ( R P S ) ) } $.
    cnoproprabco.13 $e |- F e. ( ( J tX K ) Cn L ) $.
    cnoproprabco.14 $e |- G e. ( ( J tX K ) Cn M ) $.
    cnoproprabco.15 $e |- P e. ( ( L tX M ) Cn N ) $.
    $( The composition of a continuous operation with two continuous operations
       is continuous.  (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    cnoproprabco $p |- H e. ( ( J tX K ) Cn N ) $=
      ( vw vs vt vr cv wcel wa co cop wceq copab2 ccom ctx ccn cuni cxp wf ctop
      wfn eqid txtop mp2an txuni eqcomi cnf mp3an ffn oprabval4g mpd3an3 foprcl
      ax-mp eqeltrrd opeq12d eqeq2d pm5.32i oprabbii oprab2co w3a 3pm3.2i ax-17
      hboprab1 hbxfreq hbopr hbop hbeq hban hboprab2 opreq12 anbi12d cbvoprab12
      eleq1 bi2anan9 pm3.2i txcnoprab eqeltri cnco ) MHAUTZFVAZBUTZGVAZVBZUPUTZ
      XLXNKVCZXLXNLVCZVDZVEZVBZABUPVFZVGZNOVHVCZRVIVCZHPVJZQVJZVKZVNZMYDVEYIRVJ
      ZHVLZYJPQVHVCZVMVAZRVMVAZHYMRVIVCVAZYLPVMVAZQVMVAZYNUEUFPQYMYMVOZVPVQZUGU
      OHYMRYIYKYMVJZYIYQYRUUAYIVEUEUFPQYMYGYHYSYGVOZYHVOZVRVQVSYKVOVTWAYIYKHWBW
      FABUPCFGIJYGYHYCMHXPXRIYGXMXOISVAXRIVEUHABEFGISKUJWCWDZXLXNYGFGKYEVMVAZYQ
      KYEPVIVCVAZFGVKZYGKVLNVMVAZOVMVAZUUEUCUDNOYEYEVOZVPVQZUEUMKYEPUUGYGYEVJZU
      UGUUHUUIUULUUGVEUCUDNOYEFGUUJUAUBVRVQVSZUUBVTWAWEWGXPXSJYHXMXOJTVAXSJVEUI
      ABDFGJTLUKWCWDZXLXNYHFGLUUEYRLYEQVIVCVAZUUGYHLVLUUKUFUNLYEQUUGYHUUMUUCVTW
      AWEWGYBXPXQIJVDZVEZVBABUPXPYAUUQXPXTUUPXQXPXRIXSJUUDUUNWHWIWJWKULWLWFUUEY
      NYOWMYCYEYMVIVCZVAZYPVBYDYFVAUUEYNYOUUKYTUGWNUUSYPYCUQUTZFVAZURUTZGVAZVBZ
      XQUUTUVBKVCZUUTUVBLVCZVDZVEZVBZUQURUPVFZUURYBUVIABUPUQURYBUQWOYBURWOUVDUV
      HAUVDAWOAUSUSXQUVGUSUTZXQVAZAWOAUSUVEUVFAUSUUTUVBKUVKUUTVAZAWOZAUSKXPEUTI
      VEVBZABEVFZUJUVOABEUSWPWQUVKUVBVAZAWOZWRAUSUUTUVBLUVNAUSLXPDUTJVEVBZABDVF
      ZUKUVSABDUSWPWQUVRWRWSWTXAUVDUVHBUVDBWOBUSUSXQUVGUVLBWOBUSUVEUVFBUSUUTUVB
      KUVMBWOZBUSKUVPUJUVOABEUSXBWQUVQBWOZWRBUSUUTUVBLUWABUSLUVTUKUVSABDUSXBWQU
      WBWRWSWTXAXLUUTVEZXNUVBVEZVBZXPUVDYAUVHUWCXMUVAUWDXOUVCXLUUTFXFXNUVBGXFXG
      UWEXTUVGXQUWEXRUVEXSUVFXLUUTXNUVBKXCXLUUTXNUVBLXCWHWIXDXEYQYRVBUUHUUIVBUU
      FUUOVBUVJUURVAYQYRUEUFXHUUHUUIUCUDXHUUFUUOUMUNXHUQURUPPQYMNKLUVJOFGYEYSUA
      UBUUJUVJVOXIWAXJUOXHYCHYEYMRXKVQXJ $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d J x y z $.  $d R u z $.  $d S v z $.  $d F z $.  $d G z $.
    $d P x y z $.  $d u v x y $.
    cnoproprabcoc.1 $e |- J = ( MetOpen ` ( abs o. - ) ) $.
    cnoproprabcoc.2 $e |- R e. _V $.
    cnoproprabcoc.3 $e |- S e. _V $.
    cnoproprabcoc.4 $e |- F = { <. <. x , y >. , u >. |
                                      ( ( x e. CC /\ y e. CC ) /\ u = R ) } $.
    cnoproprabcoc.5 $e |- G = { <. <. x , y >. , v >. |
                                      ( ( x e. CC /\ y e. CC ) /\ v = S ) } $.
    cnoproprabcoc.6 $e |- H = { <. <. x , y >. , z >. |
                              ( ( x e. CC /\ y e. CC ) /\ z = ( R P S ) ) } $.
    cnoproprabcoc.7 $e |- F e. ( ( J tX J ) Cn J ) $.
    cnoproprabcoc.8 $e |- G e. ( ( J tX J ) Cn J ) $.
    cnoproprabcoc.9 $e |- P e. ( ( J tX J ) Cn J ) $.
    $( The composition of a continuous operation with two continuous operations
       is continuous.  Version for operations ` ( CC X. CC ) --> CC ` .
       (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    cnoproprabcoc $p |- H e. ( ( J tX J ) Cn J ) $=
      ( cvv cuni cabs cmin ccom cme wcel wceq eqid cnmet cnmetba uniopn2 eqcomi
      cc ax-mp ctop opntop cv wa a1i cnoproprabco ) ABCDEUOUOFGHIJKLLLLLUBUBLUC
      ZUOUDUEUFZUGUHZVCUOUIVDVDUJZUKZVDLUOVDVFULMUMUPUNZVHVELUQUHVGVDLMURUPZVIV
      IVIVIGUBUHAUSUOUHBUSUOUHUTZNVAHUBUHVJOVAPQRSTUAVB $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d A u v w x y z $.  $d B u v x y z $.  $d F t u v y z $.  $d G v $.
    $d R u w $.  $d t w x $.
    cnoprab.1 $e |- A = U. J $.
    cnoprab.2 $e |- B = U. K $.
    cnoprab.3 $e |- J e. Top $.
    cnoprab.4 $e |- K e. Top $.
    cnoprab.5 $e |- L e. Top $.
    cnoprab.6 $e |- ( x e. A -> R e. X ) $.
    cnoprab.7 $e |- F = { <. x , w >. | ( x e. A /\ w = R ) } $.

    ${
      cnoprab1.8 $e |- G = { <. <. x , y >. , z >. |
                                        ( ( x e. A /\ y e. B ) /\ z = R ) } $.
      cnoprab1.9 $e |- F e. ( J Cn L ) $.
      $( Continuity of an operation which is a function in only the first
         variable.  (Contributed by Jeff Madsen, 11-Jun-2010.) $)
      cnoprab1 $p |- G e. ( ( J tX K ) Cn L ) $=
        ( vu vv vt c1st cxp cres ccom ctx co ccn wfn wceq cuni wf ctop wcel cnf
        eqid mp3an ffn ax-mp cv simpl copab2 cvv wss wfo fo1st fofn ssv fnssres
        wa mp2an fnoprv oprvres cop cfv df-opr visset op1st eqtri syl6eq eqeq2d
        mpbi pm5.32i oprabbii ax-17 copab hbopab1 hbxfreq hbfv hbeq hban anbi1d
        eleq1 wb fveq2 fveq1i fvopab2 syl5eq sylan9eq adantrd pm5.32d cbvoprab1
        mpdan bitrd eqtr4i oprabco w3a txtop 3pm3.2i tx1cn pm3.2i cnco eqeltri
        ex ) IHUFEFUGZUHZUIZJKUJUKZLULUKZHEUMZIYAUNELUOZHUPZYDJUQURZLUQURZHJLUL
        UKURZYFPRUBHJLEYENYEUTUSVAEYEHVBVCUCBUDCEFUCVDZEXTIHYJEURZBVDZFURZVEXTY
        KYMVNZUDVDZYJYLXTUKZUNZVNZUCBUDVFZYNYOYJUNZVNZUCBUDVFXTXSUMZXTYSUNUFVGU
        MZXSVGVHUUBVGVGUFVIUUCVJVGVGUFVKVCXSVLVGXSUFVMVOUCBUDEFXTVPWFYRUUAUCBUD
        YNYQYTYNYPYJYOYNYPYJYLUFUKZYJYJYLEFUFVQUUDYJYLVRUFVSYJYJYLUFVTYJYLUCWAW
        BWCWDWEWGWHWCIAVDZEURZYMVNZCVDZGUNZVNZABCVFYNUUHYJHVSZUNZVNZUCBCVFUAUUM
        UUJUCBCAYNUULAYNAWIAUEUEUUHUUKUEVDZUUHURAWIAUEYJHAUEHUUFDVDGUNVNZADWJZT
        UUOADUEWKWLUUNYJURAWIWMWNWOUUJUCWIYJUUEUNZUUMUUGUULVNUUJUUQYNUUGUULUUQY
        KUUFYMYJUUEEWQWPWPUUQUUGUULUUIUUQUUFUULUUIWRZYMUUQUUFUURUUQUUFVNUUKGUUH
        UUQUUFUUKUUEHVSZGYJUUEHWSUUFUUSUUEUUPVSZGUUEHUUPTWTUUFGMURUUTGUNSADEGMX
        AXGXBXCWEXRXDXEXHXFXIXJVCYBUQURZYGYHXKXTYBJULUKURZYIVNYAYCURUVAYGYHYGKU
        QURZUVAPQJKYBYBUTZXLVOPRXMUVBYIYGUVCUVBPQJKYBEFXSUVDNOXSUTXNVOUBXOXTHYB
        JLXPVOXQ $.
        $( [11-Jun-2010] $)
    $}

    ${
      cnoprab2.8 $e |- G = { <. <. y , x >. , z >. |
                                        ( ( y e. B /\ x e. A ) /\ z = R ) } $.
      cnoprab2.9 $e |- F e. ( J Cn L ) $.
      $( Continuity of an operation which is a function in only the second
         variable.  (Contributed by Jeff Madsen, 11-Jun-2010.) $)
      cnoprab2 $p |- G e. ( ( K tX J ) Cn L ) $=
        ( vu vv vt c2nd cxp cres ccom ctx co ccn wfn wceq cuni wf ctop wcel cnf
        eqid mp3an ffn ax-mp cv simpr copab2 cvv wss wfo fo2nd fofn ssv fnssres
        wa mp2an fnoprv oprvres cop cfv df-opr visset op2nd eqtri syl6eq eqeq2d
        mpbi pm5.32i oprabbii ax-17 copab hbopab1 hbxfreq hbfv hbeq hban anbi2d
        eleq1 anbi1d fveq2 fveq1i fvopab2 mpdan syl5eq sylan9eq adantld pm5.32d
        wb ex bitrd cbvoprab2 eqtr4i oprabco txtop 3pm3.2i tx2cn pm3.2i eqeltri
        w3a cnco ) IHUFFEUGZUHZUIZKJUJUKZLULUKZHEUMZIYBUNELUOZHUPZYEJUQURZLUQUR
        ZHJLULUKURZYGPRUBHJLEYFNYFUTUSVAEYFHVBVCBUCUDCFEUCVDZEYAIHBVDZFURZYKEUR
        ZVEYAYMYNVNZUDVDZYLYKYAUKZUNZVNZBUCUDVFZYOYPYKUNZVNZBUCUDVFYAXTUMZYAYTU
        NUFVGUMZXTVGVHUUCVGVGUFVIUUDVJVGVGUFVKVCXTVLVGXTUFVMVOBUCUDFEYAVPWFYSUU
        BBUCUDYOYRUUAYOYQYKYPYOYQYLYKUFUKZYKYLYKFEUFVQUUEYLYKVRUFVSYKYLYKUFVTYL
        YKBWAUCWAWBWCWDWEWGWHWCIYMAVDZEURZVNZCVDZGUNZVNZBACVFYOUUIYKHVSZUNZVNZB
        UCCVFUAUUNUUKBUCCAYOUUMAYOAWIAUEUEUUIUULUEVDZUUIURAWIAUEYKHAUEHUUGDVDGU
        NVNZADWJZTUUPADUEWKWLUUOYKURAWIWMWNWOUUKUCWIYKUUFUNZUUNUUHUUMVNUUKUURYO
        UUHUUMUURYNUUGYMYKUUFEWQWPWRUURUUHUUMUUJUURUUGUUMUUJXGZYMUURUUGUUSUURUU
        GVNUULGUUIUURUUGUULUUFHVSZGYKUUFHWSUUGUUTUUFUUQVSZGUUFHUUQTWTUUGGMURUVA
        GUNSADEGMXAXBXCXDWEXHXEXFXIXJXKXLVCYCUQURZYHYIXRYAYCJULUKURZYJVNYBYDURU
        VBYHYIKUQURZYHUVBQPKJYCYCUTZXMVOPRXNUVCYJUVDYHUVCQPKJYCFEXTUVEONXTUTXOV
        OUBXPYAHYCJLXSVOXQ $.
        $( [11-Jun-2010] $)
    $}
  $}

  ${
    $d F y z $.  $d R w $.  $d w x y z $.
    cnoprabc.1 $e |- J = ( MetOpen ` ( abs o. - ) ) $.
    cnoprabc.2 $e |- R e. _V $.
    cnoprabc.3 $e |- F = { <. x , w >. | ( x e. CC /\ w = R ) } $.
    cnoprabc.4 $e |- F e. ( CC -cn-> CC ) $.

    ${
      cnoprab1c.5 $e |- G = { <. <. x , y >. , z >. |
                                      ( ( x e. CC /\ y e. CC ) /\ z = R ) } $.
      $( Continuity of an operation which is a function in only the first
         variable.  Version for operations ` ( CC X. CC ) --> CC ` .
         (Contributed by Jeff Madsen, 11-Jun-2010.) $)
      cnoprab1c $p |- G e. ( ( J tX J ) Cn J ) $=
        ( cc cvv cuni cabs wcel ax-mp co cmin ccom cme wceq eqid cnmetba eqcomi
        cnmet uniopn2 ctop opntop cv a1i ccncf ccn cncfmet1 eleqtri cnoprab1 )
        ABCDNNEFGHHHOHPZNQUAUBZUCRZUSNUDUTUTUEZUHZUTHNUTVBUFIUISUGZVDVAHUJRVCUT
        HIUKSZVEVEEORAULNRJUMKMFNNUNTHHUOTLUTHVBIUPUQUR $.
        $( [11-Jun-2010] $)
    $}

    ${
      cnoprab2c.5 $e |- G = { <. <. y , x >. , z >. |
                                      ( ( y e. CC /\ x e. CC ) /\ z = R ) } $.
      $( Continuity of an operation which is a function in only the second
         variable.  Version for operations ` ( CC X. CC ) --> CC ` .
         (Contributed by Jeff Madsen, 11-Jun-2010.) $)
      cnoprab2c $p |- G e. ( ( J tX J ) Cn J ) $=
        ( cc cvv cuni cabs wcel ax-mp co cmin ccom cme wceq eqid cnmetba eqcomi
        cnmet uniopn2 ctop opntop cv a1i ccncf ccn cncfmet1 eleqtri cnoprab2 )
        ABCDNNEFGHHHOHPZNQUAUBZUCRZUSNUDUTUTUEZUHZUTHNUTVBUFIUISUGZVDVAHUJRVCUT
        HIUKSZVEVEEORAULNRJUMKMFNNUNTHHUOTLUTHVBIUPUQUR $.
        $( [11-Jun-2010] $)
    $}
  $}

  ${
    $d M b c d s t u v w x y z $.  $d N b c d s t u v x y z $.
    $d X b c d r s t u v w x y z $.  $d Y b c d r s t u v x y z $.
    $d R a r s t u v x y z $.  $d S a r s t u v x y z $.
    $d U a r s t u v x y z $.
    txmet.1 $e |- R = ( MetOpen ` M ) $.
    txmet.2 $e |- S = ( MetOpen ` N ) $.
    txmet.3 $e |- T = ( R tX S ) $.
    txmet.4 $e |- X = dom dom M $.
    txmet.5 $e |- Y = dom dom N $.
    txmet.6 $e |- M e. Met $.
    txmet.7 $e |- N e. Met $.
    txmet.8 $e |- U = { <. <. b , c >. , d >. | ( ( b e. ( X X. Y ) /\
              c e. ( X X. Y ) ) /\ d = sup ( { ( ( 1st ` b ) M ( 1st ` c ) ) ,
                              ( ( 2nd ` b ) N ( 2nd ` c ) ) } , RR , < ) ) } $.
    $( The product of two metric space topologies, in terms of the sup norm.
       (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    txmet $p |- T = ( MetOpen ` U ) $=
      ( wa vz vu vv vx vy va vr vs vt vw cv cxp wceq wrex ctg cfv cbl ctop wcel
      co cme opntop ax-mp mp2an wss wi wral cuni eqid uniopn2 eqcomi ctb visset
      eqeq1 2rexbidv elab cc0 clt wbr cr cop wex elxp opnss mpan ssel2 sylan wb
      isopn4 simprbi opreq1 sseq1d anbi2d rexbidv rcla4cva jca cle cif ad2ant2l
      ad2ant2r id ad2ant2lr mp3an1 an4s syl2an an4 jctl adantr adantll ad2antlr
      ssbl syl31anc sstr2 adantl ad2antrr c2nd elxp6 anbi1i bitri metcl anim12i
      syl c1st wn ltnle ancoms ifbid syl6eq breq1d bitrd elbl mpanl1 ex anbi12d
      cdm anassrs imp syl5ibrcom wrel dmex cab crn ctx txval eqtri unieqi txuni
      copn txbas unitg 3eqtr3ri eleq2i reeanv ifcl ltmin biimpar ad3antrrr min1
      0re min2 anass w3a 3adant3 ifswap maxlt 3expa adantrr an32s biimpd syl6ib
      pm5.32da anim2d syl5bi opelxpi metxp cpr csup supex dmoprab2 dmeqi dmxpid
      eqtr2i op1st op2nd metxpdval pm5.32d adantlr syl5bb 3imtr4d sylan2 xpss12
      ltso ssrdv syl2im syl2and impr syl2anb opreq2 rcla4ev syl12anc r19.23advv
      breq2 syl5bir com23 imp3a 19.23advv blssex mpbird eleq2 sseq2 rgen2 blbas
      imbi12d unirnbl eqtr3i 3eqtr2ri tgss2 mp3an mpbir blrn2 xp1st blopn xp2nd
      wal elbl2 mp3anl1 bicomd bi2anan9 expcom anidms com12 3bitrd exp3a impcom
      blssm sseld anim12d pm4.71rd bitr4d fveq2 opreq1d bibi12d syl5ibr expdimp
      crab eleq2d 19.23aivv sylbi elrab opelxp 19.21aivv ssrab2 relxp relss mp2
      3bitr4g eqrel sylibr xpeq1 eqeq2d rcla42ev syl111anc cvv elisseti eqeltri
      xpeq2 xpex rabex eleq1 expimpd r19.23aivv ssriv tgss eqssi tgbl 3eqtri )
      CUAUKZUBUKZUCUKZULZUMZUCBUNUBAUNZUAUUAZUOUPZDUQUPZUUBZUOUPZDUUHUPZCABUUCU
      TZVWDNAURUSZBURUSZVWIVWDUMEVAUSZVWJQEALVBVCZFVAUSZVWKRFBMVBVCZUAUCUBABUUD
      VDZUUEVWDVWGVWDVWGVEZUDUKZUEUKZUSZVWRUFUKZUSZVXAVWSVEZTZUFVWFUNZVFZUEVWCV
      GUDVWCVHZVGZVXFUDUEVXGVWCVWRVXGUSVWRGHULZUSZVWSVVTUMZUCBUNUBAUNZVXFVWSVWC
      USVXGVXIVWRVWIVHZVWDVHZVXIVXGVWIVWDVWPUUFZVWJVWKVXMVXIUMVWMVWOABVWIGHVWIV
      IAVHZGVWLVXPGUMQEAGOLVJVCVKBVHZHVWNVXQHUMRFBHPMVJVCVKUUGVDZVWCVLUSZVXNVXG
      UMVWJVWKVXSVWMVWOUAUCUBABUUIVDZVWCUUJVCZUUKUULVWBVXLUAVWSUEVMVVQVWSUMVWAV
      XKUBUCABVVQVWSVVTVNVOVPVXJVXLVXFVXJVXKVXFUBUCABVXJVVRAUSZVVSBUSZTZVXKVXFV
      FVXJVYDTZVXFVXKVWRVVTUSZVXBVXAVVTVEZTZUFVWFUNZVFVYEVYFVYIVYEVYFTVYIVQUGUK
      ZVRVSZVWRVYJVWEUTZVVTVEZTZUGVTUNZVYEVYFVYOVYFVWRUHUKZUIUKZWAZUMZVYPVVRUSZ
      VYQVVSUSZTZTZUIWBUHWBVYEVYOUHUIVWRVVRVVSWCVYEWUCVYOUHUIVYDWUCVYOVFVXJVYDV
      YSWUBVYOVYDWUBVYSVYOVYDWUBVYSVYOVFZVYBVYTVYCWUAWUDVYBVYTTZVYCWUATZTVYOVYS
      VYKVYRVYJVWEUTZVVTVEZTZUGVTUNZWUEVYPGUSZVQVWRVRVSZVYPVWREUQUPZUTZVVRVEZTZ
      UDVTUNZTVYQHUSZVQVWSVRVSZVYQVWSFUQUPZUTZVVSVEZTZUEVTUNZTWUJWUFWUEWUKWUQVY
      BVVRGVEZVYTWUKVWLVYBWVEQVVREAGOLWDWEVVRGVYPWFWGVYBWULUJUKZVWRWUMUTZVVRVEZ
      TZUDVTUNZUJVVRVGZVYTWUQVYBWVEWVKVWLVYBWVEWVKTWHQUJUDVVREAGOLWIVCWJWVJWUQU
      JVYPVVRWVFVYPUMZWVIWUPUDVTWVLWVHWUOWULWVLWVGWUNVVRWVFVYPVWRWUMWKWLWMWNWOW
      GWPWUFWURWVDVYCVVSHVEZWUAWURVWNVYCWVMRVVSFBHPMWDWEVVSHVYQWFWGVYCWUSVVQVWS
      WUTUTZVVSVEZTZUEVTUNZUAVVSVGZWUAWVDVYCWVMWVRVWNVYCWVMWVRTWHRUAUEVVSFBHPMW
      IVCWJWVQWVDUAVYQVVSVVQVYQUMZWVPWVCUEVTWVSWVOWVBWUSWVSWVNWVAVVSVVQVYQVWSWU
      TWKWLWMWNWOWGWPWUKWURWUQWVDWUJWUKWURTZWUQWVDTZWUJWWAWUPWVCTZUEVTUNUDVTUNW
      VTWUJWUPWVCUDUEVTVTUUMWVTWWBWUJUDUEVTVTWVTVWRVTUSZVWSVTUSZTZWWBWUJVFZWUKW
      WCWURWWDWWFWUKWWCTZWURWWDTZTZWWBWUJWWGWUPWWHWVCWUJWWGWUPTZWWHWVCTZTVWRVWS
      WQVSZVWRVWSWRZVTUSZVQWWMVRVSZVYRWWMVWEUTZVVTVEZWUJWWGWWHWWNWUPWVCWWCWWDWW
      NWUKWURWWLVWRVWSVTUUNZWSWTWWJWWCWULTZWWDWUSTZWWOWWKWWCWULWWSWUKWUOWWSXAZX
      BWWDWUSWWTWURWVBWWTXAZXBWWCWWDWULWUSWWOWWEWWOWULWUSTZVQVTUSWWCWWDWWOWXCWH
      UUSVQVWRVWSUUOXCUUPZXDXEWWGWWHWUPWVCWWQWWIWVTWWETZWXCWUOWVBTZTWWQWWBWUKWW
      CWURWWDXFWULWUOWUSWVBXFWXEWXCWXFWWQWXEWXCTZWUOVYPWWMWUMUTZVVRVEZWVBVYQWWM
      WUTUTZVVSVEZWWQWXGWXHWUNVEZWUOWXIVFWXGVWLWUKTZWWNWWOTZWWSWWMVWRWQVSZWXLWU
      KWXMWURWWEWXCWUKVWLQXGUUQWWEWXCWXNWVTWWEWXCTZWWNWWOWWEWWNWXCWWRXHWXDWPZXI
      ZWWEWXCWWSWVTWWCWULWWSWWDWUSWXAWTXIWWEWXOWVTWXCVWRVWSUURXJEVYPWWMVWRGOXKX
      LWXHWUNVVRXMYBWXGWXJWVAVEZWVBWXKVFWXGVWNWURTZWXNWWTWWMVWSWQVSZWXSWVTWXTWW
      EWXCWURWXTWUKWURVWNRXGXNXOWXRWWEWXCWWTWVTWWDWUSWWTWWCWULWXBWSXIWWEWYAWVTW
      XCVWRVWSUUTXJFVYQWWMVWSHPXKXLWXJWVAVVSXMYBWXGWWPWXHWXJULZVEWXIWXKTWYBVVTV
      EWWQWXGUAWWPWYBWVTWWEWXCVVQWWPUSZVVQWYBUSZVFZWXPWVTWXNWYEWXQWVTWXNTZVVQVX
      IUSZVYQVVQXPUPZFUTZVYPVVQYCUPZEUTZVRVSZWYKWYIWRZWWMVRVSZTZVVQWYJWYHWAUMZW
      YJGUSZWYKWWMVRVSZTZWYHHUSZWYIWWMVRVSZTZTZTZWYCWYDWYOWYPWYQWYTTZWYNTZTZWYF
      XUDWYOWYPXUETZWYNTXUGWYGXUHWYNVVQGHXQXRWYPXUEWYNUVAXSWYFXUFXUCWYPWYFXUFXU
      EWYRXUATZTZXUCWYFXUFXUJWYFXUEWYNXUIWVTXUEWXNWYNXUIWHZWVTXUETZWWNXUKWWOXUL
      WYKVTUSZWYIVTUSZTZWWNXUKWUKWYQWURWYTXUOWUKWYQTXUMWURWYTTXUNVWLWUKWYQXUMQV
      YPWYJEGOXTXCVWNWURWYTXUNRVYQWYHFHPXTXCYAXDXUMXUNWWNXUKXUMXUNWWNUVBZWYNWYK
      WYIWQVSZWYIWYKWRZWWMVRVSXUIXUPWYMXURWWMVRXUPWYMXUQYDZWYKWYIWRXURXUPWYLXUS
      WYKWYIXUMXUNWYLXUSWHZWWNXUNXUMXUTWYIWYKYEYFUVCYGXUQWYKWYIUVDYHYIWYKWYIWWM
      UVEYJUVFWGUVGUVHUVKUVIWYQWYTWYRXUAXFUVJUVLUVMWYFWYCWYGVYRVVQDUTZWWMVRVSZT
      ZWYOWVTVYRVXIUSZWXNWYCXVCWHZVYPVYQGHUVNZDVAUSZXVDWXNXVEIJKEFDGHOPQRSUVOZV
      VQDVYRWWMVXIDYOZYOVXIVXIULZYOVXIXVIXVJIJKVXIVXIIUKZYCUPJUKZYCUPEUTXVKXPUP
      XVLXPUPFUTUVPZVTVRUVQDVTXVMVRUWLUVRSUVSUVTVXIUWAUWBZYKYLWGWYFWYGXVBWYNWVT
      WYGXVBWYNWHZVFZWXNWVTXVDXVPXVFXVDWYGXVOXVDWYGTXVAWYMWWMVRIJKEFDVYRVVQVYPV
      YQWYJWYHGHOPQRSVYRYCUPZVYPVYPVYQUHVMZUWCZVKZVYRXPUPZVYQVYPVYQXVRUIVMUWDZV
      KZWYJVIWYHVIUWEYIYMYBXHUWFYJWYDWYPWYJWXHUSZWYHWXJUSZTZTWYFXUDVVQWXHWXJXQW
      YFXWFXUCWYPWYFXWDWYSXWEXUBWUKWXNXWDWYSWHZWURVWLWUKWXNXWGQWYJEVYPWWMGOYKYL
      UWGWURWXNXWEXUBWHZWUKVWNWURWXNXWHRWYHFVYQWWMHPYKYLXIYNWMUWHUWIUWJYPUWMWXH
      VVRWXJVVSUWKWWPWYBVVTXMUWNUWOUWPUWQXDWUIWWOWWQTUGWWMVTVYJWWMUMZVYKWWOWUHW
      WQVYJWWMVQVRUXBXWIWUGWWPVVTVYJWWMVYRVWEUWRWLYNUWSUWTXDYMXDYMUXAUXCYQXDXEV
      YSVYNWUIUGVTVYSVYMWUHVYKVYSVYLWUGVVTVWRVYRVYJVWEWKWLWMWNYRXDYMUXDUXEXNUXF
      UVMYQVXJVYIVYOWHZVYDVYFXVGVXJXWJXVHUFUGVVTDVWRVXIXVNUXGWEXOUXHYMVXKVWTVYF
      VXEVYIVWSVVTVWRUXIVXKVXDVYHUFVWFVXKVXCVYGVXBVWSVVTVXAUXJWMWNUXMYRYMUXAYQU
      WQUXKVXSVWFVLUSZVXGVWFVHZUMVWQVXHWHVXTXVGXWKXVHDVWHVWHVIZUXLVCZXWLVXIVXNV
      XGXVGXWLVXIUMXVHDVXIXVNUXNVCVXMVXNVXIVXOVXRUXOVYAUXPUDUEUFVWCVWFUXQUXRUXS
      XWKVXSVWFVWCVEVWGVWDVEXWNVXTUFVWFVWCVXAVWFUSZVYKVXAVWRVWSDUTZVYJVRVSZUEVX
      IVUEZUMZTZUGVTUNUDVXIUNZVXAVWCUSZXVGXWOXXAWHXVHUDUGUEVXADVXIXVNUXTVCXWTXX
      BUDUGVXIVTVXJVYJVTUSZTZVYKXWSXXBXXDVYKTZXXBXWSXWRVWCUSZXXEXWRVVTUMZUCBUNU
      BAUNZXXFXXEVWRYCUPZVYJWUMUTZAUSZVWRXPUPZVYJWUTUTZBUSZXWRXXJXXMULZUMZXXHVX
      JXXCVYKXXKVXJXXIGUSZXXCVYKTZXXKVWRGHUYAVWLXXQXXRXXKQEXXIVYJAGOLUYBYLWGYPV
      XJXXCVYKXXNVXJXXLHUSZXXRXXNVWRGHUYCVWNXXSXXRXXNRFXXLVYJBHPMUYBYLWGYPXXEVV
      RVVSWAZXWRUSZXXTXXOUSZWHZUCUYDUBUYDZXXPXXEXYCUBUCXXEVVRGUSZVVSHUSZTZVWRXX
      TDUTZVYJVRVSZTZVVRXXJUSZVVSXXMUSZTZXYAXYBVXJXXCVYKXYJXYMWHZVXJXXRXYNVXJVY
      SWVTTZUIWBUHWBXXRXYNVFZUHUIVWRGHWCXYOXYPUHUIVYSWVTXXRXYNWVTXXRTZXYNVYSXYG
      VYRXXTDUTZVYJVRVSZTZVVRVYPVYJWUMUTZUSZVVSVYQVYJWUTUTZUSZTZWHXYQXYTXYGYUET
      YUEXYQXYGXYSYUEXXRWVTXYGXYSYUEWHZVFXXRWVTXYGYUFWVTXYGTZXXRYUFYUGXXRTZXYSV
      YQVVSFUTZVYPVVREUTZVRVSZYUJYUIWRZVYJVRVSYUJYUIWQVSZYUIYUJWRZVYJVRVSZYUEYU
      HXYRYULVYJVRYUGXYRYULUMZXXRWVTXVDXXTVXIUSZYUPXYGXVFVVRVVSGHUVNIJKEFDVYRXX
      TVYPVYQVVRVVSGHOPQRSXVTXWCXXTYCUPVVRVVRVVSUBVMZUWCVKXXTXPUPVVSVVRVVSYURUC
      VMZUWDVKUWEXEXHYIYUHYULYUNVYJVRYUGYULYUNUMXXRYUGYULYUMYDZYUJYUIWRYUNYUGYU
      KYUTYUJYUIWUKXYEWURXYFYUKYUTWHZWURXYFTZWUKXYETZYVAYVBYUIVTUSZYUJVTUSZYVAY
      VCVWNWURXYFYVDRVYQVVSFHPXTXCZVWLWUKXYEYVEQVYPVVREGOXTXCZYUIYUJYEXEYFXDYGY
      UMYUJYUIUVDYHXHYIYUHYUOYUJVYJVRVSZYUIVYJVRVSZTZYUEYUGYVEYVDTZXXRYUOYVJWHZ
      WUKXYEWURXYFYVKYVCYVEYVBYVDYVGYVFYAXDYVKXXCYVLVYKYVEYVDXXCYVLYUJYUIVYJUVE
      UVFUVGWGYUGXXRYVJYUEWHZWUKXYEWURXYFXXRYVMVFXXRYVCYVBTZYVMXXRYVNYVMVFYVNXX
      RXXRTYVMYVCXXRYVBXXRYVMYVCXXRTZYVHYUBYVBXXRTZYVIYUDYVOYUBYVHVWLWUKXYEXXRY
      UBYVHWHQVVREVYPVYJGOUYEUYFUYGYVPYUDYVIVWNWURXYFXXRYUDYVIWHRVVSFVYQVYJHPUY
      EUYFUYGUYHXDUYIUYJUYKXDYQYJUYLUYIUYMUYNUWFXYQYUEXYGXYQYUBXYEYUDXYFXYQYUAG
      VVRWUKXXRYUAGVEZWURVWLWUKXXRYVQQEVYPVYJGOUYOYLUWGUYPXYQYUCHVVSWURXXRYUCHV
      EZWUKVWNWURXXRYVRRFVYQVYJHPUYOYLXIUYPUYQUYRUYSVYSXYJXYTXYMYUEVYSXYIXYSXYG
      VYSXYHXYRVYJVRVWRVYRXXTDWKYIWMVYSXYKYUBXYLYUDVYSXXJYUAVVRVYSXXIVYPVYJWUMV
      YSXXIXVQVYPVWRVYRYCUYTXVSYHVUAVUFVYSXXMYUCVVSVYSXXLVYQVYJWUTVYSXXLXWAVYQV
      WRVYRXPUYTXWBYHVUAVUFYNVUBVUCVUDVUGVUHYQYPXYAYUQXYITXYJXWQXYIUEXXTVXIVWSX
      XTUMXWPXYHVYJVRVWSXXTVWRDUWRYIVUIYUQXYGXYIVVRVVSGHYUSVUJXRXSVVRVVSXXJXXMY
      USVUJVUPVUKXWRYSZXXOYSXXPXYDWHXWRVXIVEVXIYSYVSXWQUEVXIVULGHVUMXWRVXIVUNVU
      OXXJXXMVUMUBUCXWRXXOVUQVDVURXXGXXPXWRXXJVVSULZUMUBUCXXJXXMABVVRXXJUMVVTYV
      TXWRVVRXXJVVSVUSVUTVVSXXMUMYVTXXOXWRVVSXXMXXJVVFVUTVVAVVBVWBXXHUAXWRXWQUE
      VXIGHGEYOZYOVVCOYWAEEVAQVVDYTYTVVEHFYOZYOVVCPYWBFFVARVVDYTYTVVEVVGVVHVVQX
      WRUMVWAXXGUBUCABVVQXWRVVTVNVOVPVURVXAXWRVWCVVIYRVVJVVKVUHVVLVWFVWCVVMUXRV
      VNXVGVWGVWHUMXVHDVWHXWMVVOVCVVP $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d C x y z $.  $d J x y z $.
    txcc.1 $e |- C = ( abs o. - ) $.
    txcc.2 $e |- J = ( MetOpen ` C ) $.
    $( The product of two copies of ` CC ` , in terms of the sup norm.
       (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    txcc $p |- ( J tX J ) = ( MetOpen ` { <. <. x , y >. , z >. | ( ( x e.
          ( CC X. CC ) /\ y e. ( CC X. CC ) ) /\ z = sup ( { ( ( 1st ` x ) C
            ( 1st ` y ) ) , ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) } ) $=
      ( ctx co cv cc cxp wcel wa c1st cfv c2nd cpr cr eqid clt csup wceq copab2
      cnmetba cnmet txmet ) EEEEHIZAJZKKLZMBJZUJMNCJUIOPUKOPDIUIQPUKQPDIRSUAUBU
      CNABCUDZDDKKABCGGUHTDFUEZUMDFUFZUNULTUG $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d C x y z $.  $d J x y z $.
    addcntx.1 $e |- C = ( abs o. - ) $.
    addcntx.2 $e |- J = ( MetOpen ` C ) $.
    $( Continuity of addition, written using topological products.
       (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    addcntx $p |- + e. ( ( J tX J ) Cn J ) $=
      ( vx vy vz cv cc cxp wcel wa c1st cfv co c2nd cpr cr clt csup wceq copab2
      ctx eqid txcc addcn ) EFGAEHZIIJZKFHZUHKLGHUGMNUIMNAOUGPNUIPNAOQRSTUALEFG
      UBZBBBUCOCUJUDDEFGABCDUEUF $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d C x y z $.  $d J x y z $.
    subcntx.1 $e |- C = ( abs o. - ) $.
    subcntx.2 $e |- J = ( MetOpen ` C ) $.
    $( Continuity of subtraction, written using topological products.
       (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    subcntx $p |- - e. ( ( J tX J ) Cn J ) $=
      ( vx vy vz cv cc cxp wcel wa c1st cfv co c2nd cpr cr clt csup wceq copab2
      ctx eqid txcc subcn ) EFGAEHZIIJZKFHZUHKLGHUGMNUIMNAOUGPNUIPNAOQRSTUALEFG
      UBZBBBUCOCUJUDDEFGABCDUEUF $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d C x y z $.  $d J x y z $.
    mulcntx.1 $e |- C = ( abs o. - ) $.
    mulcntx.2 $e |- J = ( MetOpen ` C ) $.
    $( Continuity of multiplication, written using topological products.
       (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    mulcntx $p |- x. e. ( ( J tX J ) Cn J ) $=
      ( vx vy vz cv cc cxp wcel wa c1st cfv co c2nd cpr cr clt csup wceq copab2
      ctx eqid txcc mulcn ) EFGAEHZIIJZKFHZUHKLGHUGMNUIMNAOUGPNUIPNAOQRSTUALEFG
      UBZBBBUCOCUJUDDEFGABCDUEUF $.
      $( [11-Jun-2010] $)
  $}

