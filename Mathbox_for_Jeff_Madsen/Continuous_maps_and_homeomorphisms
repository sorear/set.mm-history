$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Continuous maps and homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.
    constcncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = A ) } $.
    $( A constant function is a continuous function on ` CC ` . $)
    constcncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( cc wcel cabs cmin ccom copn cfv ccn co ccncf cme csn wf eqid cnmet a1i
      id cxp fconstg cv wceq wa copab fconstopab eqtr4i feq1i sylibr cnmetba
      metcnconst syl22anc cncfmet1 syl6eleqr ) CFGZDHIJZKLZUTMNZFFONURUSPGZVBUR
      FCQZDRZDVAGVBURUSUSSZTUAZVFURUBURFVCFVCUCZRVDFCFUDFVCDVGDAUEFGBUECUFUGABU
      HVGEABFCUIUJUKULCUSUSDUTUTFFUSVEUMZVHUTSZVIUNUOUSUTVEVIUPUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y $.  $d F u v w $.
    addccncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( x + A ) ) } $.
    $( Adding a constant is a continuous function. $)
    addccncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vv vw cc wcel wss ccncf co ssid cv caddc wral wf axaddcl ancoms
      r19.21aiva fopab2 sylib crp wa wi simpr a1i cfv cmin cabs clt wbr wceq
      opreq1 oprex fvopab4 ad2antrl ad2antll opreq12d pnpcan2 3expa eqtrd
      adantrr fveq2d breq1d exbiri elcncf1di mp2ani ) CIJZIIKZVKDIILMJINZVLVJFG
      HIIDGOZVJAOZCPMZIJZAIQIIDRVJVPAIVNIJVJVPVNCSTUAABIIVODEUBUCFOZIJZVMUDJZUE
      VSUFVJVRVSUGUHVJVRHOZIJZUEZVSUEZVQDUIZVTDUIZUJMZUKUIZVMULUMVQVTUJMZUKUIZV
      MULUMVJWCUEZWGWIVMULWJWFWHUKVJWBWFWHUNVSVJWBUEZWFVQCPMZVTCPMZUJMZWHWKWDWL
      WEWMUJVRWDWLUNVJWAABVQVOWLIDVNVQCPUOEVQCPUPUQURWAWEWMUNVJVRABVTVOWMIDVNVT
      CPUOEVTCPUPUQUSUTWBVJWNWHUNZVRWAVJWOVQVTCVAVBTVCVDVEVFVGVHVI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y $.
    idcncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = x ) } $.
    $( The identity function is a continuous function on ` CC ` . $)
    idcncf $p |- F e. ( CC -cn-> CC ) $=
      ( cc0 cc wcel ccncf co 0cn cv wceq wa copab caddc addid1 eqcomd eqeq2d
      pm5.32i opabbii eqtri addccncf ax-mp ) EFGCFFHIGJABECCAKZFGZBKZUDLZMZABNU
      EUFUDEOIZLZMZABNDUHUKABUEUGUJUEUDUIUFUEUIUDUDPQRSTUAUBUC $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d A u v w x y $.  $d F u v w $.
    sub1cncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( x - A ) ) } $.
    $( Subtracting a constant is a continuous function. $)
    sub1cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vw vv cc wcel wss ccncf co ssid cv cmin wral wf subcl ancoms
      r19.21aiva fopab2 sylib crp wa wi simpr a1i cfv cabs clt wbr wceq opreq1
      oprex fvopab4 ad2antrl ad2antll opreq12d nnncan2 3expa eqtrd fveq2d
      adantrr breq1d exbiri elcncf1di mp2ani ) CIJZIIKZVJDIILMJINZVKVIFGHIIDGOZ
      VIAOZCPMZIJZAIQIIDRVIVOAIVMIJVIVOVMCSTUAABIIVNDEUBUCFOZIJZVLUDJZUEVRUFVIV
      QVRUGUHVIVQHOZIJZUEZVRUEZVPDUIZVSDUIZPMZUJUIZVLUKULVPVSPMZUJUIZVLUKULVIWB
      UEWFWHVLUKVIWAWFWHUMVRVIWAUEZWEWGUJWIWEVPCPMZVSCPMZPMZWGWIWCWJWDWKPVQWCWJ
      UMVIVTABVPVNWJIDVMVPCPUNEVPCPUOUPUQVTWDWKUMVIVQABVSVNWKIDVMVSCPUNEVSCPUOU
      PURUSWAVIWLWGUMZVQVTVIWMVPVSCUTVATVBVCVDVEVFVGVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y $.  $d F u v w $.
    sub2cncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( A - x ) ) } $.
    $( Subtraction from a constant is a continuous function. $)
    sub2cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vw vv cc wcel wss ccncf co ssid cv cmin wral wf subcl r19.21aiva
      fopab2 sylib crp wa wi simpr a1i cfv cabs clt wbr wceq opreq2 oprex
      fvopab4 ad2antrl ad2antll opreq12d nnncan1 3expb eqtrd fveq2d abssub
      ancoms adantl adantrr breq1d exbiri elcncf1di mp2ani ) CIJZIIKZVLDIILMJIN
      ZVMVKFGHIIDGOZVKCAOZPMZIJZAIQIIDRVKVQAICVOSTABIIVPDEUAUBFOZIJZVNUCJZUDVTU
      EVKVSVTUFUGVKVSHOZIJZUDZVTUDZVRDUHZWADUHZPMZUIUHZVNUJUKVRWAPMUIUHZVNUJUKV
      KWDUDWHWIVNUJVKWCWHWIULVTVKWCUDZWHWAVRPMZUIUHZWIWJWGWKUIWJWGCVRPMZCWAPMZP
      MZWKWJWEWMWFWNPVSWEWMULVKWBABVRVPWMIDVOVRCPUMECVRPUNUOUPWBWFWNULVKVSABWAV
      PWNIDVOWACPUMECWAPUNUOUQURVKVSWBWOWKULCVRWAUSUTVAVBWCWLWIULZVKWBVSWPWAVRV
      CVDVEVAVFVGVHVIVJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $( The composition of two continuous maps on complex numbers is also
       continuous. $)
    cncfco $p |- ( ( ( A C_ CC /\ B C_ CC /\ C C_ CC ) /\ ( F e. ( A -cn-> B )
                /\ G e. ( B -cn-> C ) ) ) -> ( G o. F ) e. ( A -cn-> C ) ) $=
      ( cc wss w3a ccncf co wcel wa ccom wi cabs cmin cxp cres copn cfv ccn
      ctop cme eqid cnmet metres ax-mp opntop cnco ex mp3an wceq cncfmet
      3adant3 eleq2d 3adant1 anbi12d 3adant2 imbi12d mpbiri imp ) AFGZBFGZCFGZH
      ZDABIJZKZEBCIJZKZLZEDMZACIJZKZVEVJVMNDOPMZAAQRZSTZVNBBQRZSTZUAJZKZEVRVNCC
      QRZSTZUAJZKZLZVKVPWBUAJZKZNZVPUBKZVRUBKZWBUBKZWHVOUCKZWIVNUCKZWLVNVNUDUEZ
      VNAUFUGVOVPVPUDZUHUGVQUCKZWJWMWPWNVNBUFUGVQVRVRUDZUHUGWAUCKZWKWMWRWNVNCUF
      UGWAWBWBUDZUHUGWIWJWKHWEWGDEVPVRWBUIUJUKVEVJWEVMWGVEVGVTVIWDVEVFVSDVBVCVF
      VSULVDABVOVQVPVRVOUDZVQUDZWOWQUMUNUOVEVHWCEVCVDVHWCULVBBCVQWAVRWBXAWAUDZW
      QWSUMUPUOUQVEVLWFVKVBVDVLWFULVCACVOWAVPWBWTXBWOWSUMURUOUSUTVA $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J x y z $.  $d K x y z $.  $d X x y z $.  $d Y x y z $.  $d A x y z $.
    $d F x y z $.
    cnimass.1 $e |- X = U. J $.
    cnimass.2 $e |- Y = U. K $.
    $( Restriction of the image of a continuous function. $)
    cnimass $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) ) /\
                              ( A C_ Y /\ A. x e. X ( F ` x ) e. A ) )
                            -> F e. ( J Cn ( subSp ` <. A , K >. ) ) ) $=
      ( vy vz ctop wcel ccn co w3a wss cv cfv wral wa cop csubsp cuni wf ccnv
      cima wb sseq2i stoig3 sylan2b eqid iscn sylan2 anassrs adantrr 3adantl3
      wfn crn cnf adantr ffn syl fnfvrnss sylan adantrl wceq stoig2 3ad2antl2
      sseqtr4d df-f sylanbrc cin wrex cvv uniexg syl5eqel ssexg ancoms visset
      issubspt mp3an2 syldan wfun fnfun 3syl ad2antrr inpreima imassrn a1i cdm
      dfdm4 fndm syl5eqr sseqtrd ssid funimass5 syl11anc biimpar sstrd df-ss
      sylib eqtrd cnima adantlr eqeltrd imaeq2 eleq1d syl5ibrcom r19.23adva
      sylbid r19.21aiv mpbir2and ) DLMZELMZCDENOMZPZBGQZARCSBMAFTZUAZUAZCDBEUBU
      CSZNOMZFYBUDZCUEZCUFZJRZUGZDMZJYBTZXNXOXTYCYEYJUAUHZXPXNXOUAXRYKXSXNXOXRY
      KXOXRUAXNYBLMZYKXRXOBEUDZQZYLGYMBIUIZBEUJUKJCDYBFYDHYDULUMUNUOUPUQYACFURZ
      CUSZYDQYEYAFGCUEZYPXQYRXTCDEFGHIUTZVAFGCVBZVCYAYQBYDXQXSYQBQZXRXQYPXSUUAX
      QYRYPYSYTVCAFBCVDVEVFXOXNXTYDBVGZXPXOXRUUBXSXRXOYNUUBYOBEVHUKUPVIVJFYDCVK
      VLYAYIJYBYAYGYBMZYGKRZBVMZVGZKEVNZYIXOXNXTUUCUUGUHZXPXOXRUUHXSXOXRBVOMZUU
      HXOGVOMZXRUUIXOGYMVOIELVPVQXRUUJUUIBGVOVRVSVEXOYGVOMUUIUUHJVTKYGBVOEWAWBW
      CUPVIYAUUFYIKEYAUUDEMZUAZYIUUFYFUUEUGZDMUULUUMYFUUDUGZDUULUUMUUNYFBUGZVMZ
      UUNUULCWDZUUMUUPVGXQUUQXTUUKXQYRYPUUQYSYTFCWEWFZWGUUDBCWHVCUULUUNUUOQUUPU
      UNVGUULUUNFUUOXQUUNFQXTUUKXQUUNYFUSZFUUNUUSQXQYFUUDWIWJXQUUSCWKZFCWLXQYRY
      PUUTFVGYSYTFCWMWFZWNWOWGYAFUUOQZUUKXQXSUVBXRXQUVBXSXQUUQFUUTQUVBXSUHUURXQ
      FFUUTFFQXQFWPWJUVAVJAFBCWQWRWSVFVAWTUUNUUOXAXBXCXQUUKUUNDMXTUUDCDEXDXEXFU
      UFYHUUMDYGUUEYFXGXHXIXJXKXLXM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J y z $.  $d K y z $.  $d F y z $.  $d X y z $.  $d A y z $.
    cnres.1 $e |- X = U. J $.
    $( The restriction of a continuous function to a subset is continuous. $)
    cnres $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( F e. ( J Cn K ) /\ A C_ X ) )
                        -> ( F |` A ) e. ( ( subSp ` <. A , J >. ) Cn K ) ) $=
      ( vz vy wss ctop wcel wa ccn co cuni cres cop csubsp cfv sseq2i biimpi
      wf ccnv cv cima wral wb stoig3 eqid iscn sylan an32s adantrl cnf 3expa
      adantrr simprr fssres syl11anc stoig2 feq2d ad2ant2rl mpbird cin wceq
      wrex wi w3a cnima ex imp adantlrr anim1i anasss wfn ffn wfun fnfun
      respreima syl ad2antrr sylanl1 ineq1 eqeq2d rcla4ev cvv simpl resexg
      cnvexg imaexg 3syl ad2antrl uniexg ssexg sylan2 ancoms issubspt
      syl111anc adantlr adantr r19.21aiva mpbir2and sylanr2 ) AEIZCJKZDJKZLZBCD
      MNZKZACOZIZBAPZACQRSZDMNKZXDXKEXJAFTUAXGXIXKLZLZXNXMOZDOZXLUBZXLUCZGUDZUE
      ZXMKZGDUFZXGXKXNXSYDLUGZXIXEXKXFYEXEXKLZXMJKXFYEACUHGXLXMDXQXRXQUIXRUIZUJ
      UKULUMXPXSAXRXLUBZXPXJXRBUBZXKYHXGXIYIXKXEXFXIYIBCDXJXRXJUIYGUNUOZUPXGXIX
      KUQXJXRABURUSXEXKXSYHUGXFXIYFXQAXRXLACUTVAVBVCXPYCGDXPYADKZLZYCYBHUDZAVDZ
      VEZHCVFZYLBUCYAUEZCKZYBYQAVDZVEZYPXGXIYKYRXKXGXILZYKYRXEXFXIYKYRVGXEXFXIV
      HYKYRYABCDVIVJUOVKVLXPYIXKLZYKYTXGXIXKUUBUUAYIXKYJVMVNYIBXJVOZXKYKYTXJXRB
      VPUUCYTXKYKUUCBVQYTXJBVRYAABVSVTWAWBUKYOYTHYQCYMYQVEYNYSYBYMYQAWCWDWEUSXP
      YCYPUGZYKXEXOUUDXFXEXOLXEYBWFKZAWFKZUUDXEXOWGXIUUEXEXKXIXLWFKXTWFKUUEBAXH
      WHXLWFWIXTYAWFWJWKWLXEXKUUFXIXKXEUUFXEXKXJWFKUUFCJWMAXJWFWNWOWPUMHYBAWFCW
      QWRWSWTVCXAXBXC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J x y z $.  $d K x y z $.  $d F x y z $.  $d X x y z $.  $d Y x y z $.
    $d A x y z $.  $d B x y z $.
    cnres2.1 $e |- X = U. J $.
    cnres2.2 $e |- Y = U. K $.
    $( The restriction of a continuous function to a subset is continuous. $)
    cnres2 $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( A C_ X /\ B C_ Y ) /\
                ( F e. ( J Cn K ) /\ A. x e. A ( F ` x ) e. B ) ) -> ( F |` A )
                  e. ( ( subSp ` <. A , J >. ) Cn ( subSp ` <. B , K >. ) ) )
        $=
      ( ctop wcel wa wss ccn co cv cfv wral w3a cop csubsp cres cuni sseq2i
      stoig3 sylan2b ad2ant2r 3adant3 simp1r simp1l simp3l simp2l cnres
      syl22anc simp2r wceq stoig2 adantr raleqdv fvres eleq1d ralbiia syl6rbb
      biimpa anasss 3impa eqid cnimass syl32anc ) EKLZFKLZMZBGNZCHNZMZDEFOPLZAQ
      ZDRZCLZABSZMZTZBEUAUBRZKLZVLDBUCZWDFOPLZVOVRWFRZCLZAWDUDZSZWFWDCFUAUBROPL
      VMVPWEWBVKVNWEVLVOVNVKBEUDZNZWEGWLBIUEZBEUFUGUHUIVKVLVPWBUJZWCVKVLVQVNWGV
      KVLVPWBUKWOVMVPVQWAULVMVNVOWBUMBDEFGIUNUOVMVNVOWBUPVMVPWBWKVMVPMZVQWAWKWP
      VQMZWAWKWQWKWIABSWAWQWIAWJBWPWJBUQZVQVKVNWRVLVOVNVKWMWRWNBEURUGUHUSUTWIVT
      ABVRBLWHVSCVRBDVAVBVCVDVEVFVGACWFWDFWJHWJVHJVIVJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J x y z $.  $d K x y z $.  $d F x y z $.
    $( A continuous function is continuous onto its image. $)
    cnresima $p |- ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) )
                              -> F e. ( J Cn ( subSp ` <. ran F , K >. ) ) ) $=
      ( vx vy vz ctop wcel ccn co crn cop csubsp cfv wa cuni wf ccnv cv cima
      wral eqid iscn biimpa wb simpll wss frn stoig3 sylan2 ad2ant2lr syl11anc
      wfn ffn dffn3 sylib adantl wceq stoig2 feq3 syl mpbird cin wrex cvv
      uniexg fex ancoms rnexg visset issubspt mp3an2 anassrs ancom1s adantrr
      wi imaeq2 eleq1d rcla4cva elin fnfvelrn simpl pm3.21 impbid2 syl5bb
      pm5.32da elpreima 3bitr4d eqrdv biimpar syl5ibrcom r19.23adva anasss
      sylbid r19.21aiv mpbir2and syldan 3impa ) BGHZCGHZABCIJHZABAKZCLMNZIJHZWS
      WTOZXABPZCPZAQZARZDSZTZBHZDCUAZOZXDXEXAXNDABCXFXGXFUBZXGUBUCUDXEXNOZXDXFX
      CPZAQZXIESZTZBHZEXCUAZXPWSXCGHZXDXRYBOUEWSWTXNUFWTXHYCWSXMXHWTXBXGUGZYCXF
      XGAUHZXBCUIUJUKEABXCXFXQXOXQUBUCULWTXHXRWSXMWTXHOZXRXFXBAQZXHYGWTXHAXFUMZ
      YGXFXGAUNZXFAUOUPUQYFXQXBURZXRYGUEXHWTYDYJYEXBCUSUJXQXBXFAUTVAVBUKXPYAEXC
      XPXSXCHZXSFSZXBVCZURZFCVDZYAXEXHYKYOUEZXMWTWSXHYPWTWSXHYPWSXHOZWTXBVEHZYP
      YQAVEHZYRXHWSYSWSXHXFVEHYSBGVFXFXGVEAVGUJVHAVEVIVAWTXSVEHYRYPEVJFXSXBVECV
      KVLUJVMVNVOXEXHXMYOYAVPXEXHOZXMOYNYAFCYTXMYLCHZYNYAVPZXMUUAOYTXIYLTZBHZUU
      BXLUUDDYLCXJYLURXKUUCBXJYLXIVQVRVSYTUUDOYAYNXIYMTZBHZYTUUFUUDYTUUEUUCBXHU
      UEUUCURZXEXHYHUUGYIYHDUUEUUCYHXJXFHZXJANZYMHZOUUHUUIYLHZOXJUUEHXJUUCHYHUU
      HUUJUUKUUJUUKUUIXBHZOZYHUUHOZUUKUUIYLXBVTUUNUULUUMUUKUEXFXJAWAUULUUMUUKUU
      KUULWBUULUUKWCWDVAWEWFXFXJYMAWGXFXJYLAWGWHWIVAUQVRWJYNXTUUEBXSYMXIVQVRWKU
      JVMWLWMWNWOWPWQWR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J u v $.  $d K u v $.  $d Y u v $.  $d A u v $.  $d F u v $.
    cnss.1 $e |- Y = U. K $.
    $( A continuous function into a subspace is continuous into the larger
       space. $)
    cnss $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( A C_ Y /\
              F e. ( J Cn ( subSp ` <. A , K >. ) ) ) ) -> F e. ( J Cn K ) ) $=
      ( vv vu ctop wcel wa wss cop csubsp cfv ccn co cuni wf ccnv cv cima wral
      wceq wb sseq2i stoig2 sylan2b adantll feq3 syl anbi1d wi fss expcom
      adantrd adantl cin wfun ffun adantr ad2antlr inpreima crn imassrn a1i
      cdm dfdm4 fdm syl5eqr sseqtrd fimacnv sseqtr4d df-ss sylib eqtr2d wrex
      eqid ineq1 eqeq2d rcla4ev mpan2 cvv uniexg syl5eqel ssexg sylan2 ancoms
      visset inex1 issubspt mp3an2 syldan mpbird adantlll imaeq2 eleq1d
      rcla4va sylan an32s adantlrl eqeltrd r19.21aiva ex jcad sylbid stoig3
      iscn anassrs 3imtr4d impr ) CIJZDIJZKZAELZBCADMNOZPQJZBCDPQJZXNXOKZCRZXPR
      ZBSZBTZGUAZUBZCJZGXPUCZKZXTEBSZYCHUAZUBZCJZHDUCZKZXQXRXSYHXTABSZYGKZYNXSY
      BYOYGXSYAAUDZYBYOUEXMXOYQXLXOXMADRZLZYQEYRAFUFZADUGUHUIYAAXTBUJUKULXSYPYI
      YMXOYPYIUMXNXOYOYIYGYOXOYIXTAEBUNUOUPUQXSYPYMXSYPKZYLHDUUAYJDJZKZYKYCYJAU
      RZUBZCUUCUUEYKYCAUBZURZYKUUCBUSZUUEUUGUDYPUUHXSUUBYOUUHYGXTABUTVAVBYJABVC
      UKUUCYKUUFLZUUGYKUDYPUUIXSUUBYOUUIYGYOYKXTUUFYOYKYCVDZXTYKUUJLYOYCYJVEVFY
      OUUJBVGXTBVHXTABVIVJVKXTABVLVMVAVBYKUUFVNVOVPXSYGUUBUUECJZYOXSUUBYGUUKXSU
      UBKUUDXPJZYGUUKXMXOUUBUULXLXMXOKZUUBKUULUUDYDAURZUDZGDVQZUUBUUPUUMUUBUUDU
      UDUDZUUPUUDVRUUOUUQGYJDYDYJUDUUNUUDUUDYDYJAVSVTWAWBUQUUMUULUUPUEZUUBXMXOA
      WCJZUURXOXMUUSXMXOEWCJUUSXMEYRWCFDIWDWEAEWCWFWGWHXMUUDWCJUUSUURYJAHWIWJGU
      UDAWCDWKWLWMVAWNWOYFUUKGUUDXPYDUUDUDYEUUECYDUUDYCWPWQWRWSWTXAXBXCXDXEXFXL
      XMXOXQYHUEZUUMXLXPIJZUUTXOXMYSUVAYTADXGUHGBCXPXTYAXTVRZYAVRXHWGXIXNXRYNUE
      XOHBCDXTEUVBFXHVAXJXK $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J y $.  $d K y $.  $d A y $.  $d B y $.  $d X y $.  $d Y y $.  $d F y $.
    paste.1 $e |- X = U. J $.
    paste.2 $e |- Y = U. K $.
    $( Pasting lemma.  If ` A ` and ` B ` are closed sets in ` X ` with
       ` A u. B = X ` , then any function whose restrictions to ` A ` and ` B `
       are continuous is continuous on all of ` X ` . $)
    paste $p |- ( ( ( J e. Top /\ K e. Top ) /\
            ( A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) /\ ( A u. B ) = X ) /\
              ( F : X --> Y /\ ( F |` A ) e. ( ( subSp ` <. A , J >. ) Cn K )
                      /\ ( F |` B ) e. ( ( subSp ` <. B , J >. ) Cn K ) ) )
                                          -> F e. ( J Cn K ) ) $=
      ( vy ctop wcel wa ccld cfv cun wceq w3a wf cres cop csubsp ccn co ccnv
      cv cima wral wb iscncl 3ad2ant1 simp1 3ad2ant3 cin wss crn imassrn a1i
      cdm dfdm4 fdm syl5eqr sseqtrd df-ss biimpi eqcomd syl adantl ineq2
      eqcoms adantr wfun ffun respreima uneq12d indi syl6reqr 3eqtrd 3ad2antr1
      3ad2antl3 3adant1 simpl1l wi cuni eqid cldss stoig3 syldan cnclima ex
      3expia sylan an32s imp subspcld2 adantlr syld 3ad2antr2 3impia 3ad2antr3
      uncld syl111anc eqeltrd r19.21aiva mpbir2and ) DKLZEKLZMZADNOZLZBXILZABPZ
      FQZRZFGCSZCATZADUAUBOZEUCUDLZCBTZBDUAUBOZEUCUDLZRZRZCDEUCUDLZXOCUEZJUFZUG
      ZXILZJENOZUHZXHXNYDXOYJMUIYBJCDEFGHIUJUKYBXHXOXNXOXRYAULUMYCYHJYIYCYFYILZ
      MZYGXPUEYFUGZXSUEYFUGZPZXIYCYGYOQZYKXNYBYPXHXMXJYBYPXKXMXRXOYPYAXMXOMYGYG
      FUNZYGXLUNZYOXOYGYQQZXMXOYGFUOZYSXOYGYEUPZFYGUUAUOXOYEYFUQURXOUUACUSFCUTF
      GCVAVBVCYTYQYGYTYQYGQYGFVDVEVFVGVHXMYQYRQZXOUUBFXLFXLYGVIVJVKXOYRYOQXMXOY
      OYGAUNZYGBUNZPZYRXOCVLZYOUUEQFGCVMUUFYMUUCYNUUDYFACVNYFBCVNVOVGYGABVPVQVH
      VRVSVTWAVKYLXFYMXILZYNXILZYOXILXFXGXNYBYKWBYCYKUUGXHXNYBYKUUGWCZXHXKXJYBU
      UIWCXMXHXJMZYBUUIUUJXOXRUUIYAUUJXRMYKYMXQNOLZUUGUUJXRYKUUKWCZXFXJXGXRUULW
      CZXFXJMXQKLZXGUUMXFXJADWDZUOUUNADUUOUUOWEZWFADWGWHUUNXGXRUULUUNXGXRRYKUUK
      YFXPXQEWIWJWKWLWMWNUUJUUKUUGWCZXRXFXJUUQXGXFXJUUKUUGAYMDWOWKWPVKWQWRWJVSW
      SWNYCYKUUHXHXNYBYKUUHWCZXHXJXKYBUURWCXMXHXKMZYBUURUUSXOYAUURXRUUSYAMYKYNX
      TNOLZUUHUUSYAYKUUTWCZXFXKXGYAUVAWCZXFXKMXTKLZXGUVBXFXKBUUOUOUVCBDUUOUUPWF
      BDWGWHUVCXGYAUVAUVCXGYARYKUUTYFXSXTEWIWJWKWLWMWNUUSUUTUUHWCZYAXFXKUVDXGXF
      XKUUTUUHBYNDWOWKWPVKWQWTWJWRWSWNYMYNDXAXBXCXDXE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d R y $.  $d S y $.  $d J x y $.
    $d K x y $.  $d L x y $.  $d M x y $.
    piececn.1 $e |- A e. RR $.
    piececn.2 $e |- B e. RR $.
    piececn.3 $e |- C e. ( A [,] B ) $.
    piececn.4 $e |- R e. _V $.
    piececn.5 $e |- S e. _V $.
    piececn.6 $e |- F = { <. x , y >. | ( x e. ( A [,] B )
                                            /\ y = if ( x <_ C , R , S ) ) } $.
    piececn.7 $e |- J = ( subSp ` <. ( A [,] B ) , ( topGen ` ran (,) ) >. ) $.
    piececn.8 $e |- K = ( subSp ` <. ( A [,] C ) , ( topGen ` ran (,) ) >. ) $.
    piececn.9 $e |- L = ( subSp ` <. ( C [,] B ) , ( topGen ` ran (,) ) >. ) $.
    piececn.10 $e |- ( x = C -> R = S ) $.
    piececn.11 $e |- G = { <. x , y >. | ( x e. ( A [,] C ) /\ y = R ) } $.
    piececn.12 $e |- H = { <. x , y >. | ( x e. ( C [,] B ) /\ y = S ) } $.
    piececn.13 $e |- M e. Top $.
    piececn.14 $e |- G e. ( K Cn M ) $.
    piececn.15 $e |- H e. ( L Cn M ) $.
    $( Piecewise definition of a continuous function on a real interval. $)
    piececn $p |- F e. ( J Cn M ) $=
      ( ctop wcel wa cicc co ccld cfv cun wceq w3a cuni wf cres cop csubsp ccn
      cioo crn ctg wss retop cr iccssre mp2an uniretop sseqtr4i stoig3 eqeltri
      pm3.2i sselii clint3 cle wbr leidi wb elicc2 mpbi simp3i iccss mp3an
      eqcomi subspcld mp4an fveq2i eleqtrri simp2i iccsplit 3pm3.2i cv cif wo
      eleq2i elun bitri simp3bi iftrue syl copab fveq1i cvv fvopab2 mpan2
      syl5eq eqtr4d unieqi stoig2 eqtr2i eqid cnf ffvelrni eqeltrd clt leloe
      mpan biimpa wn ltnle iffalse breq1 mpbiri eqtrd eqcoms adantl jaodan
      syldan 3adant3 sylbi jaoi fopab reseq1 ax-mp resopab2 eqeq2d pm5.32i
      opabbii 3eqtri eqtr4i subspabs opeq2i 3eqtr4i opreq1i paste ) KUJUKZNUJUK
      ZULCEUMUNZKUOUPZUKZEDUMUNZUUOUKZUUNUUQUQZCDUMUNZURZUSUUTNUTZHVAZHUUNVBZUU
      NKVCZVDUPZNVEUNZUKZHUUQVBZUUQKVCZVDUPZNVEUNZUKZUSHKNVEUNUKUULUUMKUUTVFVGV
      HUPZVCVDUPZUJUAUVNUJUKZUUTUVNUTZVIZUVOUJUKVJUUTVKUVQCVKUKZDVKUKZUUTVKVIZO
      PCDVLVMZVNVOZUUTUVNVPVMVQUGVRUUPUURUVAUUNUVOUOUPZUUOUVPUWAUUNUVNUOUPZUKZU
      UNUUTVIZUUNUWDUKVJUWBUVSEVKUKZUWFOUUTVKEUWBQVSZCEVTVMUVSUVTULZUVSUWHULCCW
      AWBZEDWAWBZULUWGUVSUVTOPVRZUVSUWHOUWIVRUWKUWLCOWCUWHCEWAWBZUWLEUUTUKZUWHU
      WNUWLUSZQUVSUVTUWOUWPWDOPCDEWEVMWFZWGVRCDCEWHWIZUUTUUNUVNVKUVQVKVNWJZWKWL
      KUVOUOUAWMZWNUUQUWDUUOUVPUWAUUQUWEUKZUUQUUTVIZUUQUWDUKVJUWBUWHUVTUXAUWIPE
      DVTVMUWJUWHUVTULUWNDDWAWBZULUXBUWMUWHUVTUWIPVRUWNUXCUWHUWNUWLUWQWODPWCVRC
      DEDWHWIZUUTUUQUVNVKUWSWKWLUWTWNUUTUUSUVSUVTUWOUUTUUSUROPQCDEWPWIZWJWQUVCU
      VHUVMABUUTUVBAWRZEWAWBZFGWSZHTUXFUUTUKZUXFUUNUKZUXFUUQUKZWTZUXHUVBUKZUXIU
      XFUUSUKUXLUUTUUSUXFUXEXAUXFUUNUUQXBXCUXJUXMUXKUXJUXHUXFIUPZUVBUXJUXHFUXNU
      XJUXGUXHFURZUXJUXFVKUKZCUXFWAWBZUXGUVSUWHUXJUXPUXQUXGUSWDOUWICEUXFWEVMXDU
      XGFGXEZXFZUXJUXNUXFUXJBWRZFURZULZABXGZUPZFUXFIUYCUEXHUXJFXIUKUYDFURRABUUN
      FXIXJXKXLXMUUNUVBUXFILUJUKUUMILNVEUNZUKUUNUVBIVALUUNUVNVCVDUPZUJUBUVPUUNU
      VQVIZUYFUJUKVJUUNVKUVQUVSUWHUUNVKVIOUWICEVLVMVNVOZUUNUVNVPVMVQUGUHILNUUNU
      VBLUTUYFUTZUUNLUYFUBXNUVPUYGUYIUUNURVJUYHUUNUVNXOVMXPUVBXQZXRWIXSXTUXKUXH
      UXFJUPZUVBUXKUXHGUYKUXKUXPEUXFWAWBZUXFDWAWBZUSZUXHGURZUWHUVTUXKUYNWDUWIPE
      DUXFWEVMUXPUYLUYOUYMUXPUYLEUXFYAWBZEUXFURZWTZUYOUXPUYLUYRUWHUXPUYLUYRWDUW
      IEUXFYBYCYDUXPUYPUYOUYQUXPUYPULUXGYEZUYOUXPUYPUYSUWHUXPUYPUYSWDUWIEUXFYFY
      CYDUXGFGYGXFUYQUYOUXPUYOUXFEUXFEURZUXHFGUYTUXGUXOUYTUXGEEWAWBEUWIWCUXFEEW
      AYHYIUXRXFUDYJYKYLYMYNYOYPZUXKUYKUXFUXKUXTGURZULZABXGZUPZGUXFJVUDUFXHUXKG
      XIUKVUEGURSABUUQGXIXJXKXLXMUUQUVBUXFJMUJUKUUMJMNVEUNZUKUUQUVBJVAMUUQUVNVC
      VDUPZUJUCUVPUUQUVQVIZVUGUJUKVJUUQVKUVQUWHUVTUUQVKVIUWIPEDVLVMVNVOZUUQUVNV
      PVMVQUGUIJMNUUQUVBMUTVUGUTZUUQMVUGUCXNUVPVUHVUJUUQURVJVUIUUQUVNXOVMXPUYJX
      RWIXSXTYQYPYRUVDIUVGUVDUYCIUVDUXIUXTUXHURZULABXGZUUNVBZUXJVUKULZABXGZUYCH
      VULURZUVDVUMURTHVULUUNYSYTUWGVUMVUOURUWRVUKABUUNUUTUUAYTVUNUYBABUXJVUKUYA
      UXJUXHFUXTUXSUUBUUCUUDUUEUEUUFIUYEUVGUHUVFLNVEUUNUVOVCZVDUPZUYFUVFLUVPUWA
      UWGVURUYFURVJUWBUWRUUTUUNUVNVKUWSUUGWIUVEVUQVDKUVOUUNUAUUHWMUBUUIUUJWNVQU
      VIJUVLUVIVUDJUVIVULUUQVBZUXKVUKULZABXGZVUDVUPUVIVUSURTHVULUUQYSYTUXBVUSVV
      AURUXDVUKABUUQUUTUUAYTVUTVUCABUXKVUKVUBUXKUXHGUXTVUAUUBUUCUUDUUEUFUUFJVUF
      UVLUIUVKMNVEUUQUVOVCZVDUPZVUGUVKMUVPUWAUXBVVCVUGURVJUWBUXDUUTUUQUVNVKUWSU
      UGWIUVJVVBVDKUVOUUQUAUUHWMUCUUIUUJWNVQWQUUNUUQHKNUUTUVBKUTUVOUTZUUTKUVOUA
      XNUVPUVRVVDUUTURVJUWCUUTUVNXOVMXPUYJUUKWI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A w x y z $.  $d B w x y z $.  $d C w y z $.  $d J x y $.  $d K x y $.
    $d G z $.
    cncfres.1 $e |- A C_ CC $.
    cncfres.2 $e |- B C_ CC $.
    cncfres.3 $e |- F = { <. x , y >. | ( x e. CC /\ y = C ) } $.
    cncfres.4 $e |- G = { <. x , y >. | ( x e. A /\ y = C ) } $.
    cncfres.5 $e |- ( x e. A -> C e. B ) $.
    cncfres.6 $e |- F e. ( CC -cn-> CC ) $.
    cncfres.7 $e |- J = ( MetOpen ` ( ( abs o. - ) |` ( A X. A ) ) ) $.
    cncfres.8 $e |- K = ( MetOpen ` ( ( abs o. - ) |` ( B X. B ) ) ) $.
    $( A continuous function on complex numbers restricted to a subset. $)
    cncfres $p |- G e. ( J Cn K ) $=
      ( vz vw ccncf co ccn cc wcel cres cv wceq wa copab wss resopab2 ax-mp
      reseq1 3eqtr4ri wi ssid rescncf mp3an eqeltri w3a cfv wral 3pm3.2i csb
      ax-17 visset hbcsb1 hbel hbim eleq1 csbeq1a eleq1d imbi12d chvar
      fvopab4gf mpdan eqeltrd rgen cncffvrn mp2an cabs cmin ccom cxp eqid
      cncfmet eleqtri ) GCDTUAZHIUBUAZGCUCTUAZUDZGWHUDZGFCUEZWJAUFZUCUDBUFEUGZU
      HABUIZCUEZWNCUDZWOUHABUIZWMGCUCUJZWQWSUGJWOABCUCUKULFWPUGWMWQUGLFWPCUMULM
      UNFUCUCTUAUDZWMWJUDZOUCUCUJZXCWTXAXBUOUCUPZXDJUCUCCFUQURULUSWTXCDUCUJZUTR
      UFZGVAZDUDZRCVBWKWLUOWTXCXEJXDKVCXHRCXFCUDZXGAXFEVDZDXIXJDUDZXGXJUGWREDUD
      ZUOXIXKUOARXIXKAXIAVEASSXJDASXFERVFSUFZXFUDAVEZVGZXMDUDAVEVHVIWNXFUGZWRXI
      XLXKWNXFCVJXPEXJDAXFEVKZVLVMNVNZABSXFEXJCDGXNXOXQMVOVPXRVQVRRCUCDGVSVTULW
      TXEWHWIUGJKCDWAWBWCZCCWDUEZXSDDWDUEZHIXTWEYAWEPQWFVTWG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    ishomeo2.1 $e |- X = U. J $.
    ishomeo2.2 $e |- Y = U. K $.
    $( The predicate "is a homeomorphism". $)
    ishomeo2 $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Homeo K ) <->
                          ( F : X -1-1-onto-> Y /\ A. x e. J ( F " x ) e. K /\
                                        A. x e. K ( `' F " x ) e. J ) ) ) $=
      ( ctop wcel wa chomeosm co wf1o cv cima wral ccnv w3a wi ishomeo biimpd
      3expia pm2.43d cvv wf f1of cuni uniexg syl5eqel fex syl2an 3ad2antl1
      expcom adantr biimprd com23 mpdd impbid ) CIJZDIJZKZBCDLMZJZEFBNZBAOZPDJA
      CQZBRVFPCJADQZSZVBVDVIUTVAVDVDVITUTVAVDSVDVIAVCBCDEFGHUAUBUCUDVBVIBUEJZVD
      UTVIVJTVAVIUTVJVEVGUTVJVHVEEFBUFEUEJVJUTEFBUGUTECUHUEGCIUIUJEFUEBUKULUMUN
      UOVBVJVIVDUTVAVJVIVDTUTVAVJSVDVIAUEBCDEFGHUAUPUCUQURUS $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d K v $.  $d F v $.
    $( A homeomorphism is continuous. $)
    hmeocn $p |- ( ( J e. Top /\ K e. Top ) ->
                            ( F e. ( J Homeo K ) -> F e. ( J Cn K ) ) ) $=
      ( vv ctop wcel wa cuni wf1o cv cima wral ccnv w3a wf chomeosm co ccn wi
      f1of anim1i 3adant2 a1i eqid ishomeo2 iscn 3imtr4d ) BEFCEFGZBHZCHZAIZADJ
      ZKCFDBLZAMULKBFDCLZNZUIUJAOZUNGZABCPQFABCRQFUOUQSUHUKUNUQUMUKUPUNUIUJATUA
      UBUCDABCUIUJUIUDZUJUDZUEDABCUIUJURUSUFUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $( The inverse of a homeomorphism is a homeomorphism. $)
    hmeocnv $p |- ( ( ( J e. Top /\ K e. Top ) /\ F e. ( J Homeo K ) )
                                            -> `' F e. ( K Homeo J ) ) $=
      ( ctop wcel wa chomeosm co ccnv simpr cuni wf1o ccn w3a wi f1ocnv
      3ad2ant1 adantl simpr3 wrel wceq f1orel dfrel2 sylib eleq1d biimpar
      3adant3 3jca ex adantr wb eqid hmeobc 3expa cvv cnvexg 3com12 sylan2
      3imtr4d mpd ) BDEZCDEZFZABCGHZEZFZVEAIZCBGHEZVCVEJVFBKZCKZALZABCMHZEZVGCB
      MHEZNZVJVIVGLZVNVGIZVLEZNZVEVHVCVOVSOVEVCVOVSVCVOFVPVNVRVOVPVCVKVMVPVNVIV
      JAPQRVCVKVMVNSVOVRVCVKVMVRVNVKVRVMVKVQAVLVKATVQAUAVIVJAUBAUCUDUEUFUGRUHUI
      UJVAVBVEVEVOUKVDABCVIVJVIULZVJULZUMUNVEVCVGUOEZVHVSUKZAVDUPVAVBWBWCVBVAWB
      WCUOVGCBVJVIWAVTUMUQUNURUSUT $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X x y $.  $d J x y $.  $d K x y $.  $d A x y $.  $d F x y $.
    hmeoopn.1 $e |- X = U. J $.
    $( Homeomorphisms preserve openness. $)
    hmeoopn $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( F e. ( J Homeo K ) /\
                              A C_ X ) ) -> ( A e. J <-> ( F " A ) e. K ) ) $=
      ( vx vy ctop wcel wa chomeosm co wss cima wi cv wral hmeocnb imp wceq
      imaeq2 eleq1d rcla4v syl5com adantrr ccnv hmeocna cuni wf1 wf1o eqid
      hmeomap f1of1 syl f1imacnv sylan anasss sylibd impbid ) CIJDIJKZBCDLMJZAE
      NZKKZACJZBAOZDJZVAVBVEVGPVCVAVBKZBGQZOZDJZGCRZVEVGVAVBVLGBCDSTVKVGGACVIAU
      AVJVFDVIABUBUCUDUEUFVDVGBUGZVFOZCJZVEVAVBVGVOPVCVHVMHQZOZCJZHDRZVGVOVAVBV
      SHBCDUHTVRVOHVFDVPVFUAVQVNCVPVFVMUBUCUDUEUFVDVNACVAVBVCVNAUAZVHEDUIZBUJZV
      CVTVHEWABUKZWBVAVBWCBCDEWAFWAULUMTEWABUNUOEWAABUPUQURUCUSUT $.
      $( [2-Sep-2009] $)
  $}

  ${
    hmeocld.1 $e |- X = U. J $.
    $( Homeomorphisms preserve closedness. $)
    hmeocld $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( F e. ( J Homeo K ) /\
        A C_ X ) ) -> ( A e. ( Clsd ` J ) <-> ( F " A ) e. ( Clsd ` K ) ) ) $=
      ( ctop wcel wa chomeosm co wss cdif cuni cima ccld cfv wb difss hmeoopn
      mpanr2 adantrr ccnv wfun wceq wf1o eqid hmeomap imp wfo dff1o3 simprbi
      syl imadif f1ofo foima 3syl difeq1d eqtrd eleq1d bitrd iscld2 ad2ant2rl
      simplr crn imassrn a1i forn sseqtrd adantr sylan anasss syl11anc 3bitr4d
      ) CGHZDGHZIZBCDJKHZAELZIZIZEAMZCHZDNZBAOZMZDHZACPQHZWEDPQHZWAWCBWBOZDHZWG
      VQVRWCWKRZVSVQVRWBELWLEASWBBCDEFTUAUBWAWJWFDWAWJBEOZWEMZWFWABUCUDZWJWNUEV
      QVRWOVSVQVRIZEWDBUFZWOVQVRWQBCDEWDFWDUGZUHUIZWQEWDBUJZWOEWDBUKULUMUBEABUN
      UMWAWMWDWEVQVRWMWDUEZVSWPWQWTXAWSEWDBUOZEWDBUPUQUBURUSUTVAVOVSWHWCRVPVRAC
      EFVBVCWAVPWEWDLZWIWGRVOVPVTVDVQVRVSXCWPWQVSXCWSWQXCVSWQWEBVEZWDWEXDLWQBAV
      FVGWQWTXDWDUEXBEWDBVHUMVIVJVKVLWEDWDWRVBVMVN $.
      $( [2-Sep-2009] $)
  $}

