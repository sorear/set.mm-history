$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Continuous maps and homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.
    constcncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = A ) } $.
    $( A constant function is a continuous function on ` CC ` .  (Contributed
       by Jeff Madsen, 2-Sep-2009.) $)
    constcncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( cc wcel cabs cmin ccom copn cfv ccn co ccncf cme csn wf eqid cv a1i cxp
      cnmet id fconstg wceq wa copab fconstopab eqtr4i feq1i cnmetba metcnconst
      sylibr syl22anc cncfmet1 syl6eleqr ) CFGZDHIJZKLZUTMNZFFONURUSPGZVBURFCQZ
      DRZDVAGVBURUSUSSZUCUAZVFURUDURFVCFVCUBZRVDFCFUEFVCDVGDATFGBTCUFUGABUHVGEA
      BFCUIUJUKUNCUSUSDUTUTFFUSVEULZVHUTSZVIUMUOUSUTVEVIUPUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y $.  $d F u v w $.
    addccncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( x + A ) ) } $.
    $( Adding a constant is a continuous function.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    addccncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vv vw cc wcel co cv caddc ancoms wa cfv cmin cabs clt wceq wss ccncf
      ssid wral wf ax-addcl r19.21aiva fopab2 sylib crp wi simpr a1i wbr opreq1
      oprex fvopab4 ad2antrl ad2antll pnpcan2 3expa eqtrd adantrr fveq2d breq1d
      opreq12d exbiri elcncf1di mp2ani ) CIJZIIUAZVKDIIUBKJIUCZVLVJFGHIIDGLZVJA
      LZCMKZIJZAIUDIIDUEVJVPAIVNIJVJVPVNCUFNUGABIIVODEUHUIFLZIJZVMUJJZOVSUKVJVR
      VSULUMVJVRHLZIJZOZVSOZVQDPZVTDPZQKZRPZVMSUNVQVTQKZRPZVMSUNVJWCOZWGWIVMSWJ
      WFWHRVJWBWFWHTVSVJWBOZWFVQCMKZVTCMKZQKZWHWKWDWLWEWMQVRWDWLTVJWAABVQVOWLID
      VNVQCMUOEVQCMUPUQURWAWEWMTVJVRABVTVOWMIDVNVTCMUOEVTCMUPUQUSVFWBVJWNWHTZVR
      WAVJWOVQVTCUTVANVBVCVDVEVGVHVI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y $.
    idcncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = x ) } $.
    $( The identity function is a continuous function on ` CC ` .  (Contributed
       by Jeff Madsen, 11-Jun-2010.) $)
    idcncf $p |- F e. ( CC -cn-> CC ) $=
      ( cc0 cc wcel ccncf co 0cn cv wa copab caddc addid1 eqcomd eqeq2d pm5.32i
      wceq opabbii eqtri addccncf ax-mp ) EFGCFFHIGJABECCAKZFGZBKZUDSZLZABMUEUF
      UDENIZSZLZABMDUHUKABUEUGUJUEUDUIUFUEUIUDUDOPQRTUAUBUC $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d A u v w x y $.  $d F u v w $.
    sub1cncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( x - A ) ) } $.
    $( Subtracting a constant is a continuous function.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    sub1cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vw vv cc wcel co cv cmin ancoms wa cfv cabs clt wbr wceq wss ssid wf
      ccncf subcl r19.21aiva fopab2 sylib crp wi simpr a1i opreq1 oprex fvopab4
      wral ad2antrl ad2antll opreq12d nnncan2 3expa eqtrd fveq2d adantrr breq1d
      exbiri elcncf1di mp2ani ) CIJZIIUAZVJDIIUDKJIUBZVKVIFGHIIDGLZVIALZCMKZIJZ
      AIUPIIDUCVIVOAIVMIJVIVOVMCUENUFABIIVNDEUGUHFLZIJZVLUIJZOVRUJVIVQVRUKULVIV
      QHLZIJZOZVROZVPDPZVSDPZMKZQPZVLRSVPVSMKZQPZVLRSVIWBOWFWHVLRVIWAWFWHTVRVIW
      AOZWEWGQWIWEVPCMKZVSCMKZMKZWGWIWCWJWDWKMVQWCWJTVIVTABVPVNWJIDVMVPCMUMEVPC
      MUNUOUQVTWDWKTVIVQABVSVNWKIDVMVSCMUMEVSCMUNUOURUSWAVIWLWGTZVQVTVIWMVPVSCU
      TVANVBVCVDVEVFVGVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y $.  $d F u v w $.
    sub2cncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( A - x ) ) } $.
    $( Subtraction from a constant is a continuous function.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    sub2cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vw vv cc wcel co cv cmin wa cfv cabs clt wbr wceq opreq2 wss ssid wf
      ccncf wral subcl r19.21aiva fopab2 sylib crp simpr oprex fvopab4 ad2antrl
      wi a1i ad2antll opreq12d nnncan1 3expb eqtrd fveq2d abssub ancoms adantrr
      adantl breq1d exbiri elcncf1di mp2ani ) CIJZIIUAZVLDIIUDKJIUBZVMVKFGHIIDG
      LZVKCALZMKZIJZAIUEIIDUCVKVQAICVOUFUGABIIVPDEUHUIFLZIJZVNUJJZNVTUOVKVSVTUK
      UPVKVSHLZIJZNZVTNZVRDOZWADOZMKZPOZVNQRVRWAMKPOZVNQRVKWDNWHWIVNQVKWCWHWISV
      TVKWCNZWHWAVRMKZPOZWIWJWGWKPWJWGCVRMKZCWAMKZMKZWKWJWEWMWFWNMVSWEWMSVKWBAB
      VRVPWMIDVOVRCMTECVRMULUMUNWBWFWNSVKVSABWAVPWNIDVOWACMTECWAMULUMUQURVKVSWB
      WOWKSCVRWAUSUTVAVBWCWLWISZVKWBVSWPWAVRVCVDVFVAVEVGVHVIVJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $( The composition of two continuous maps on complex numbers is also
       continuous.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    cncfco $p |- ( ( ( A C_ CC /\ B C_ CC /\ C C_ CC ) /\ ( F e. ( A -cn-> B )
                /\ G e. ( B -cn-> C ) ) ) -> ( G o. F ) e. ( A -cn-> C ) ) $=
      ( cc wss ccncf co wcel cxp cres copn cfv ccn ctop cme eqid metres ax-mp
      w3a wa ccom wi cabs cmin cnmet opntop cnco ex wceq cncfmet 3adant3 eleq2d
      mp3an 3adant1 anbi12d 3adant2 imbi12d mpbiri imp ) AFGZBFGZCFGZUAZDABHIZJ
      ZEBCHIZJZUBZEDUCZACHIZJZVEVJVMUDDUEUFUCZAAKLZMNZVNBBKLZMNZOIZJZEVRVNCCKLZ
      MNZOIZJZUBZVKVPWBOIZJZUDZVPPJZVRPJZWBPJZWHVOQJZWIVNQJZWLVNVNRUGZVNASTVOVP
      VPRZUHTVQQJZWJWMWPWNVNBSTVQVRVRRZUHTWAQJZWKWMWRWNVNCSTWAWBWBRZUHTWIWJWKUA
      WEWGDEVPVRWBUIUJUOVEVJWEVMWGVEVGVTVIWDVEVFVSDVBVCVFVSUKVDABVOVQVPVRVORZVQ
      RZWOWQULUMUNVEVHWCEVCVDVHWCUKVBBCVQWAVRWBXAWARZWQWSULUPUNUQVEVLWFVKVBVDVL
      WFUKVCACVOWAVPWBWTXBWOWSULURUNUSUTVA $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J x y z $.  $d K x y z $.  $d X x y z $.  $d Y x y z $.  $d A x y z $.
    $d F x y z $.
    cnimass.1 $e |- X = U. J $.
    cnimass.2 $e |- Y = U. K $.
    $( Restriction of the image of a continuous function.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    cnimass $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) ) /\
                              ( A C_ Y /\ A. x e. X ( F ` x ) e. A ) )
                            -> F e. ( J Cn ( subSp ` <. A , K >. ) ) ) $=
      ( vy vz ctop wcel wss cv wa cima wb cvv wceq ccn co w3a cfv wral cop cuni
      csubsp wf ccnv id topopn ssexg syl2anc stoig3 syldan eqid anassrs adantrr
      iscn sylan2 3adantl3 wfn crn cnf adantr ffn fnfvrnss sylan adantrl sseq2i
      syl stoig2 sylan2b 3ad2antl2 sseqtr4d sylanbrc cin uniexg syl5eqel ancoms
      df-f wrex elsubsp wfun fnfun 3syl ad2antrr inpreima imassrn dfdm4 syl5eqr
      fndm syl5sseq ssid syl5sseqr funimass5 syl11anc biimpar sstrd df-ss sylib
      eqtrd cnima adantlr eqeltrd imaeq2 eleq1d syl5ibrcom r19.23adva r19.21aiv
      cdm sylbid mpbir2and ) DLMZELMZCDEUAUBMZUCZBGNZAOCUDBMAFUEZPZPZCDBEUFUHUD
      ZUAUBMZFYCUGZCUIZCUJZJOZQZDMZJYCUEZXOXPYAYDYFYKPRZXQXOXPPXSYLXTXOXPXSYLXP
      XSPXOYCLMZYLXPXSBSMZYMXSXSGEMYNXPXSUKEGIULBGEUMUNBESUOUPJCDYCFYEHYEUQUTVA
      URUSVBYBCFVCZCVDZYENYFYBFGCUIZYOXRYQYACDEFGHIVEZVFFGCVGZVLYBYPBYEXRXTYPBN
      ZXSXRYOXTYTXRYQYOYRYSVLAFBCVHVIVJXPXOYAYEBTZXQXPXSUUAXTXSXPBEUGZNUUAGUUBB
      IVKBEVMVNUSVOVPFYECWBVQYBYJJYCYBYHYCMZYHKOZBVRZTZKEWCZYJXPXOYAUUCUUGRZXQX
      PXSUUHXTXPXSYNUUHXPGSMZXSYNXPGUUBSIELVSVTXSUUIYNBGSUMWAVIKYHBESWDUPUSVOYB
      UUFYJKEYBUUDEMZPZYJUUFYGUUEQZDMUUKUULYGUUDQZDUUKUULUUMYGBQZVRZUUMUUKCWEZU
      ULUUOTXRUUPYAUUJXRYQYOUUPYRYSFCWFWGZWHUUDBCWIVLUUKUUMUUNNUUOUUMTUUKUUMFUU
      NXRUUMFNYAUUJXRYGVDZUUMFYGUUDWJXRUURCXLZFCWKXRYQYOUUSFTYRYSFCWMWGZWLWNWHY
      BFUUNNZUUJXRXTUVAXSXRUVAXTXRUUPFUUSNUVAXTRUUQXRFFUUSFWOUUTWPAFBCWQWRWSVJV
      FWTUUMUUNXAXBXCXRUUJUUMDMYAUUDCDEXDXEXFUUFYIUULDYHUUEYGXGXHXIXJXMXKXN $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J y z $.  $d K z $.  $d F y z $.  $d A y z $.
    cnres.1 $e |- X = U. J $.
    $( The restriction of a continuous function to a subset is continuous.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    cnres $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( F e. ( J Cn K ) /\ A C_ X ) )
                        -> ( F |` A ) e. ( ( subSp ` <. A , J >. ) Cn K ) ) $=
      ( vz vy wss ctop wcel wa ccn cuni wf wb cvv eqid syl11anc wceq cop csubsp
      co cres cfv sseq2i biimpi ccnv cv cima wral id uniexg ssexg stoig3 syldan
      syl2anc iscn sylan an32s adantrl 3expa adantrr simprr fssres stoig2 feq2d
      cnf ad2ant2rl mpbird cin wrex wi w3a cnima imp adantlrr anim1i anasss wfn
      ffn wfun fnfun respreima syl ad2antrr sylanl1 ineq1 eqeq2d rcla4ev sylan2
      ex simpl ancoms elsubsp adantlr adantr r19.21aiva mpbir2and sylanr2 ) AEI
      ZCJKZDJKZLZBCDMUCKZACNZIZBAUDZACUAUBUEZDMUCKZXAXGEXFAFUFUGXDXEXGLZLZXJXIN
      ZDNZXHOZXHUHGUIZUJZXIKZGDUKZXDXGXJXOXSLPZXEXBXGXCXTXBXGLZXIJKZXCXTXBXGAQK
      ZYBXGXGXFQKZYCXBXGULCJUMZAXFQUNZUQACQUOUPGXHXIDXMXNXMRXNRZURUSUTVAXLXOAXN
      XHOZXLXFXNBOZXGYHXDXEYIXGXBXCXEYIBCDXFXNXFRYGVHVBZVCXDXEXGVDXFXNABVESXBXG
      XOYHPXCXEYAXMAXNXHACVFVGVIVJXLXRGDXLXPDKZLZXRXQHUIZAVKZTZHCVLZYLBUHXPUJZC
      KZXQYQAVKZTZYPXDXEYKYRXGXDXELZYKYRXBXCXEYKYRVMXBXCXEVNYKYRXPBCDVOWLVBVPVQ
      XLYIXGLZYKYTXDXEXGUUBUUAYIXGYJVRVSYIBXFVTZXGYKYTXFXNBWAUUCYTXGYKUUCBWBYTX
      FBWCXPABWDWEWFWGUSYOYTHYQCYMYQTYNYSXQYMYQAWHWIWJSXLXRYPPZYKXBXKUUDXCXBXKL
      XBYCUUDXBXKWMXBXGYCXEXGXBYCXBXGYDYCYEYFWKWNVAHXQACQWOSWPWQVJWRWSWT $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J x y z $.  $d K x y z $.  $d F x y z $.  $d X x y z $.  $d Y x y z $.
    $d A x y z $.  $d B x y z $.
    cnres2.1 $e |- X = U. J $.
    cnres2.2 $e |- Y = U. K $.
    $( The restriction of a continuous function to a subset is continuous.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    cnres2 $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( A C_ X /\ B C_ Y ) /\
                ( F e. ( J Cn K ) /\ A. x e. A ( F ` x ) e. B ) ) -> ( F |` A )
                  e. ( ( subSp ` <. A , J >. ) Cn ( subSp ` <. B , K >. ) ) )
        $=
      ( ctop wcel wa wss ccn co cfv wral cop csubsp cv w3a cres cuni cvv topopn
      id ssexg syl2anc stoig3 syldan ad2ant2r simp1r simp1l simp3l simp2l cnres
      3adant3 syl22anc simp2r sseq2i stoig2 sylan2b adantr raleqdv fvres eleq1d
      wceq ralbiia syl6rbb biimpa anasss 3impa eqid cnimass syl32anc ) EKLZFKLZ
      MZBGNZCHNZMZDEFOPLZAUAZDQZCLZABRZMZUBZBESTQZKLZVRDBUCZWJFOPLZWAWDWLQZCLZA
      WJUDZRZWLWJCFSTQOPLVSWBWKWHVQVTWKVRWAVQVTBUELZWKVTVTGELWRVQVTUGEGIUFBGEUH
      UIBEUEUJUKULURVQVRWBWHUMZWIVQVRWCVTWMVQVRWBWHUNWSVSWBWCWGUOVSVTWAWHUPBDEF
      GIUQUSVSVTWAWHUTVSWBWHWQVSWBMZWCWGWQWTWCMZWGWQXAWQWOABRWGXAWOAWPBWTWPBVHZ
      WCVQVTXBVRWAVTVQBEUDZNXBGXCBIVABEVBVCULVDVEWOWFABWDBLWNWECWDBDVFVGVIVJVKV
      LVMACWLWJFWPHWPVNJVOVP $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J x y z $.  $d K x y z $.  $d F x y z $.
    $( A continuous function is continuous onto its image.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    cnresima $p |- ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) )
                              -> F e. ( J Cn ( subSp ` <. ran F , K >. ) ) ) $=
      ( vx vy vz ctop wcel wa cuni wf cv cima eqid cvv wceq sylan2 syl eleq1d
      wb ccn co crn cop csubsp cfv ccnv wral biimpa simpll wss frn uniexg ssexg
      iscn syl2anc stoig3 syldan ad2ant2lr syl11anc wfn ffn dffn3 adantl stoig2
      sylib feq3 mpbird cin fex ancoms rnexg elsubsp anassrs ancom1s adantrr wi
      wrex imaeq2 rcla4cva elin fnfvelrn simpl pm3.21 impbid2 pm5.32da elpreima
      syl5bb 3bitr4d eqrdv biimpar syl5ibrcom anasss sylbid r19.21aiv mpbir2and
      r19.23adva 3impa ) BGHZCGHZABCUAUBHZABAUCZCUDUEUFZUAUBHZWSWTIZXABJZCJZAKZ
      AUGZDLZMZBHZDCUHZIZXDXEXAXNDABCXFXGXFNZXGNUOUIXEXNIZXDXFXCJZAKZXIELZMZBHZ
      EXCUHZXPWSXCGHZXDXRYBITWSWTXNUJWTXHYCWSXMWTXHXBOHZYCXHXBXGUKZXGOHYDWTXFXG
      AULZCGUMXBXGOUNUPXBCOUQURUSEABXCXFXQXOXQNUOUTWTXHXRWSXMWTXHIZXRXFXBAKZXHY
      HWTXHAXFVAZYHXFXGAVBZXFAVCVFVDYGXQXBPZXRYHTXHWTYEYKYFXBCVEQXQXBXFAVGRVHUS
      XPYAEXCXPXSXCHZXSFLZXBVIZPZFCVRZYAXEXHYLYPTZXMWTWSXHYQWTWSXHYQWSXHIZWTYDY
      QYRAOHZYDXHWSYSWSXHXFOHYSBGUMXFXGOAVJQVKAOVLRFXSXBCOVMQVNVOVPXEXHXMYPYAVQ
      XEXHIZXMIYOYAFCYTXMYMCHZYOYAVQZXMUUAIYTXIYMMZBHZUUBXLUUDDYMCXJYMPXKUUCBXJ
      YMXIVSSVTYTUUDIYAYOXIYNMZBHZYTUUFUUDYTUUEUUCBXHUUEUUCPZXEXHYIUUGYJYIDUUEU
      UCYIXJXFHZXJAUFZYNHZIUUHUUIYMHZIXJUUEHXJUUCHYIUUHUUJUUKUUJUUKUUIXBHZIZYIU
      UHIZUUKUUIYMXBWAUUNUULUUMUUKTXFXJAWBUULUUMUUKUUKUULWCUULUUKWDWERWHWFXFXJY
      NAWGXFXJYMAWGWIWJRVDSWKYOXTUUEBXSYNXIVSSWLQVNWQWMWNWOWPURWR $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J u v $.  $d K u v $.  $d Y u v $.  $d A u v $.  $d F u v $.
    cnss.1 $e |- Y = U. K $.
    $( A continuous function into a subspace is continuous into the larger
       space.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    cnss $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( A C_ Y /\
              F e. ( J Cn ( subSp ` <. A , K >. ) ) ) ) -> F e. ( J Cn K ) ) $=
      ( vv vu ctop wcel wa wss cuni wf cima wceq wb cin adantr cvv cop cfv ccnv
      csubsp ccn co cv wral sseq2i stoig2 sylan2b adantll feq3 anbi1d wi expcom
      syl fss adantrd wfun ffun ad2antlr inpreima crn imassrn cdm dfdm4 syl5eqr
      adantl fdm syl5sseq fimacnv sseqtr4d df-ss sylib eqtr2d wrex ineq1 eqeq2d
      rcla4ev mpan2 uniexg syl5eqel ssexg sylan2 ancoms elsubsp syldan adantlll
      eqid mpbird imaeq2 eleq1d rcla4va sylan an32s adantlrl eqeltrd r19.21aiva
      ex jcad sylbid id topopn syl2anc stoig3 iscn anassrs 3imtr4d impr ) CIJZD
      IJZKZAELZBCADUAUDUBZUEUFJZBCDUEUFJZXMXNKZCMZXOMZBNZBUCZGUGZOZCJZGXOUHZKZX
      SEBNZYBHUGZOZCJZHDUHZKZXPXQXRYGXSABNZYFKZYMXRYAYNYFXRXTAPZYAYNQXLXNYPXKXN
      XLADMZLYPEYQAFUIADUJUKULXTAXSBUMUQUNXRYOYHYLXNYOYHUOXMXNYNYHYFYNXNYHXSAEB
      URUPUSVIXRYOYLXRYOKZYKHDYRYIDJZKZYJYBYIARZOZCYTUUBYJYBAOZRZYJYTBUTZUUBUUD
      PYOUUEXRYSYNUUEYFXSABVASVBYIABVCUQYTYJUUCLZUUDYJPYOUUFXRYSYNUUFYFYNYJXSUU
      CYNYBVDZYJXSYBYIVEYNUUGBVFXSBVGXSABVJVHVKXSABVLVMSVBYJUUCVNVOVPXRYFYSUUBC
      JZYNXRYSYFUUHXRYSKUUAXOJZYFUUHXLXNYSUUIXKXLXNKZYSKUUIUUAYCARZPZGDVQZYSUUM
      UUJYSUUAUUAPZUUMUUAWJUULUUNGYIDYCYIPUUKUUAUUAYCYIAVRVSVTWAVIUUJUUIUUMQZYS
      XLXNATJZUUOXNXLUUPXLXNETJUUPXLEYQTFDIWBWCAETWDWEWFGUUAADTWGWHSWKWIYEUUHGU
      UAXOYCUUAPYDUUBCYCUUAYBWLWMWNWOWPWQWRWSWTXAXBXKXLXNXPYGQZUUJXKXOIJZUUQXLX
      NUUPUURXNXNEDJUUPXLXNXCDEFXDAEDWDXEADTXFWHGBCXOXSXTXSWJZXTWJXGWEXHXMXQYMQ
      XNHBCDXSEUUSFXGSXIXJ $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J y $.  $d K y $.  $d A y $.  $d B y $.  $d X y $.  $d Y y $.  $d F y $.
    paste.1 $e |- X = U. J $.
    paste.2 $e |- Y = U. K $.
    $( Pasting lemma.  If ` A ` and ` B ` are closed sets in ` X ` with
       ` A u. B = X ` , then any function whose restrictions to ` A ` and ` B `
       are continuous is continuous on all of ` X ` .  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    paste $p |- ( ( ( J e. Top /\ K e. Top ) /\
            ( A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) /\ ( A u. B ) = X ) /\
              ( F : X --> Y /\ ( F |` A ) e. ( ( subSp ` <. A , J >. ) Cn K )
                      /\ ( F |` B ) e. ( ( subSp ` <. B , J >. ) Cn K ) ) )
                                          -> F e. ( J Cn K ) ) $=
      ( vy ctop wcel wa ccld cfv wceq w3a cin adantr wi cun cres cop csubsp ccn
      wf co ccnv cv cima wral wb iscncl 3ad2ant1 simp1 3ad2ant3 wss crn imassrn
      cdm dfdm4 fdm syl5eqr syl5sseq df-ss biimpi eqcomd syl adantl eqcoms wfun
      ineq2 ffun respreima uneq12d syl6reqr 3ad2antr1 3ad2antl3 3adant1 simpl1l
      indi 3eqtrd stoig3 cnclima 3expia sylan an32s subspcld2 adantlr 3ad2antr2
      ex imp syld 3impia 3ad2antr3 uncld syl111anc eqeltrd r19.21aiva mpbir2and
      ) DKLZEKLZMZADNOZLZBXDLZABUAZFPZQZFGCUFZCAUBZADUCUDOZEUEUGLZCBUBZBDUCUDOZ
      EUEUGLZQZQZCDEUEUGLZXJCUHZJUIZUJZXDLZJENOZUKZXCXIXSXJYEMULXQJCDEFGHIUMUNX
      QXCXJXIXJXMXPUOUPXRYCJYDXRYAYDLZMZYBXKUHYAUJZXNUHYAUJZUAZXDXRYBYJPZYFXIXQ
      YKXCXHXEXQYKXFXHXMXJYKXPXHXJMYBYBFRZYBXGRZYJXJYBYLPZXHXJYBFUQZYNXJXTURZYB
      FXTYAUSXJYPCUTFCVAFGCVBVCVDYOYLYBYOYLYBPYBFVEVFVGVHVIXHYLYMPZXJYQFXGFXGYB
      VLVJSXJYMYJPXHXJYJYBARZYBBRZUAZYMXJCVKZYJYTPFGCVMUUAYHYRYIYSYAACVNYABCVNV
      OVHYBABWAVPVIWBVQVRVSSYGXAYHXDLZYIXDLZYJXDLXAXBXIXQYFVTXRYFUUBXCXIXQYFUUB
      TZXCXFXEXQUUDTXHXCXEMZXQUUDUUEXJXMUUDXPUUEXMMYFYHXLNOLZUUBUUEXMYFUUFTZXAX
      EXBXMUUGTZXAXEMXLKLZXBUUHADXDWCUUIXBXMUUGUUIXBXMQYFUUFYAXKXLEWDWKWEWFWGWL
      UUEUUFUUBTZXMXAXEUUJXBXAXEUUFUUBAYHDWHWEWISWMWJWKVQWNWLXRYFUUCXCXIXQYFUUC
      TZXCXEXFXQUUKTXHXCXFMZXQUUKUULXJXPUUKXMUULXPMYFYIXONOLZUUCUULXPYFUUMTZXAX
      FXBXPUUNTZXAXFMXOKLZXBUUOBDXDWCUUPXBXPUUNUUPXBXPQYFUUMYAXNXOEWDWKWEWFWGWL
      UULUUMUUCTZXPXAXFUUQXBXAXFUUMUUCBYIDWHWEWISWMWOWKWJWNWLYHYIDWPWQWRWSWT $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d R y $.  $d S y $.  $d J x y $.
    $d K x y $.  $d L x y $.  $d M x y $.
    piececn.1 $e |- A e. RR $.
    piececn.2 $e |- B e. RR $.
    piececn.3 $e |- C e. ( A [,] B ) $.
    piececn.4 $e |- R e. _V $.
    piececn.5 $e |- S e. _V $.
    piececn.6 $e |- F = { <. x , y >. | ( x e. ( A [,] B )
                                            /\ y = if ( x <_ C , R , S ) ) } $.
    piececn.7 $e |- J = ( subSp ` <. ( A [,] B ) , ( topGen ` ran (,) ) >. ) $.
    piececn.8 $e |- K = ( subSp ` <. ( A [,] C ) , ( topGen ` ran (,) ) >. ) $.
    piececn.9 $e |- L = ( subSp ` <. ( C [,] B ) , ( topGen ` ran (,) ) >. ) $.
    piececn.10 $e |- ( x = C -> R = S ) $.
    piececn.11 $e |- G = { <. x , y >. | ( x e. ( A [,] C ) /\ y = R ) } $.
    piececn.12 $e |- H = { <. x , y >. | ( x e. ( C [,] B ) /\ y = S ) } $.
    piececn.13 $e |- M e. Top $.
    piececn.14 $e |- G e. ( K Cn M ) $.
    piececn.15 $e |- H e. ( L Cn M ) $.
    $( Piecewise definition of a continuous function on a real interval.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    piececn $p |- F e. ( J Cn M ) $=
      ( ctop wcel wa cicc co ccld cfv cun wceq w3a cuni wf cres cop csubsp cioo
      ccn crn ctg cvv retop oprex stoig3 mp2an eqeltri pm3.2i cr iccssre sselii
      wss icccld cle wbr leidi elicc2 mpbi simp3i iccss mp3an uniretop subspcld
      wb eqcomi mp4an fveq2i eleqtrri simp2i iccsplit 3pm3.2i cv wo eleq2i elun
      cif bitri simp3bi iftrue syl fveq1i fvopab2 syl5eq eqtr4d unieqi sseqtr4i
      copab mpan2 stoig2 eqtr2i eqid cnf ffvelrni eqeltrd clt mpan biimpa sylbi
      reseq1 ax-mp resopab2 eqeq2d pm5.32i opabbii 3eqtri eqtr4i subspco opeq2i
      cin df-ss opeq1i 3eqtr4i opreq1i leloe wn ltnle breq1 mpbiri eqtrd eqcoms
      iffalse adantl jaodan syldan 3adant3 jaoi fopab paste ) KUJUKZNUJUKZULCEU
      MUNZKUOUPZUKZEDUMUNZUUSUKZUURUVAUQZCDUMUNZURZUSUVDNUTZHVAZHUURVBZUURKVCZV
      DUPZNVFUNZUKZHUVAVBZUVAKVCZVDUPZNVFUNZUKZUSHKNVFUNUKUUPUUQKUVDVEVGVHUPZVC
      VDUPZUJUAUVRUJUKZUVDVIUKZUVSUJUKVJCDUMVKZUVDUVRVIVLVMVNUGVOUUTUVBUVEUURUV
      SUOUPZUUSUVTUVDVPVSZUURUVRUOUPZUKZUURUVDVSZUURUWCUKVJCVPUKZDVPUKZUWDOPCDV
      QVMZUWHEVPUKZUWFOUVDVPEUWJQVRZCEVTVMUWHUWIULZUWHUWKULCCWAWBZEDWAWBZULUWGU
      WHUWIOPVOZUWHUWKOUWLVOUWNUWOCOWCUWKCEWAWBZUWOEUVDUKZUWKUWQUWOUSZQUWHUWIUW
      RUWSWKOPCDEWDVMWEZWFVOCDCEWGWHZUVDUURUVRVPUVRUTZVPWIWLZWJWMKUVSUOUAWNZWOU
      VAUWCUUSUVTUWDUVAUWEUKZUVAUVDVSZUVAUWCUKVJUWJUWKUWIUXEUWLPEDVTVMUWMUWKUWI
      ULUWQDDWAWBZULUXFUWPUWKUWIUWLPVOUWQUXGUWKUWQUWOUWTWPDPWCVOCDEDWGWHZUVDUVA
      UVRVPUXCWJWMUXDWOUVDUVCUWHUWIUWRUVDUVCUROPQCDEWQWHZWLWRUVGUVLUVQABUVDUVFA
      WSZEWAWBZFGXCZHTUXJUVDUKZUXJUURUKZUXJUVAUKZWTZUXLUVFUKZUXMUXJUVCUKUXPUVDU
      VCUXJUXIXAUXJUURUVAXBXDUXNUXQUXOUXNUXLUXJIUPZUVFUXNUXLFUXRUXNUXKUXLFURZUX
      NUXJVPUKZCUXJWAWBZUXKUWHUWKUXNUXTUYAUXKUSWKOUWLCEUXJWDVMXEUXKFGXFZXGZUXNU
      XRUXJUXNBWSZFURZULZABXNZUPZFUXJIUYGUEXHUXNFVIUKUYHFURRABUURFVIXIXOXJXKUUR
      UVFUXJILUJUKUUQILNVFUNZUKUURUVFIVALUURUVRVCZVDUPZUJUBUVTUURVIUKZUYKUJUKVJ
      CEUMVKZUURUVRVIVLVMVNUGUHILNUURUVFLUTUYKUTZUURLUYKUBXLUVTUURUXBVSUYNUURUR
      VJUURVPUXBUWHUWKUURVPVSOUWLCEVQVMWIXMUURUVRXPVMXQUVFXRZXSWHXTYAUXOUXLUXJJ
      UPZUVFUXOUXLGUYPUXOUXTEUXJWAWBZUXJDWAWBZUSZUXLGURZUWKUWIUXOUYSWKUWLPEDUXJ
      WDVMUXTUYQUYTUYRUXTUYQEUXJYBWBZEUXJURZWTZUYTUXTUYQVUCUWKUXTUYQVUCWKUWLEUX
      JUUAYCYDUXTVUAUYTVUBUXTVUAULUXKUUBZUYTUXTVUAVUDUWKUXTVUAVUDWKUWLEUXJUUCYC
      YDUXKFGUUHXGVUBUYTUXTUYTUXJEUXJEURZUXLFGVUEUXKUXSVUEUXKEEWAWBEUWLWCUXJEEW
      AUUDUUEUYBXGUDUUFUUGUUIUUJUUKUULYEZUXOUYPUXJUXOUYDGURZULZABXNZUPZGUXJJVUI
      UFXHUXOGVIUKVUJGURSABUVAGVIXIXOXJXKUVAUVFUXJJMUJUKUUQJMNVFUNZUKUVAUVFJVAM
      UVAUVRVCZVDUPZUJUCUVTUVAVIUKZVUMUJUKVJEDUMVKZUVAUVRVIVLVMVNUGUIJMNUVAUVFM
      UTVUMUTZUVAMVUMUCXLUVTUVAUXBVSVUPUVAURVJUVAVPUXBUWKUWIUVAVPVSUWLPEDVQVMWI
      XMUVAUVRXPVMXQUYOXSWHXTYAUUMYEUUNUVHIUVKUVHUYGIUVHUXMUYDUXLURZULABXNZUURV
      BZUXNVUQULZABXNZUYGHVURURZUVHVUSURTHVURUURYFYGUWGVUSVVAURUXAVUQABUURUVDYH
      YGVUTUYFABUXNVUQUYEUXNUXLFUYDUYCYIYJYKYLUEYMIUYIUVKUHUVJLNVFUURUVSVCZVDUP
      ZUURUVDYPZUVRVCZVDUPZUVJLUVTUYLUWAVVDVVGURVJUYMUWBUURUVDUVRVIVIYNWHUVIVVC
      VDKUVSUURUAYOWNLUYKVVGUBVVFUYJVDVVEUURUVRUWGVVEUURURUXAUURUVDYQWEYRWNYMYS
      YTWOVNUVMJUVPUVMVUIJUVMVURUVAVBZUXOVUQULZABXNZVUIVVBUVMVVHURTHVURUVAYFYGU
      XFVVHVVJURUXHVUQABUVAUVDYHYGVVIVUHABUXOVUQVUGUXOUXLGUYDVUFYIYJYKYLUFYMJVU
      KUVPUIUVOMNVFUVAUVSVCZVDUPZUVAUVDYPZUVRVCZVDUPZUVOMUVTVUNUWAVVLVVOURVJVUO
      UWBUVAUVDUVRVIVIYNWHUVNVVKVDKUVSUVAUAYOWNMVUMVVOUCVVNVULVDVVMUVAUVRUXFVVM
      UVAURUXHUVAUVDYQWEYRWNYMYSYTWOVNWRUURUVAHKNUVDUVFKUTUVSUTZUVDKUVSUAXLUVTU
      VDUXBVSVVPUVDURVJUVDVPUXBUWJWIXMUVDUVRXPVMXQUYOUUOWH $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d A w x y z $.  $d B w x y z $.  $d C w y z $.  $d J x y $.  $d K x y $.
    $d G z $.
    cncfres.1 $e |- A C_ CC $.
    cncfres.2 $e |- B C_ CC $.
    cncfres.3 $e |- F = { <. x , y >. | ( x e. CC /\ y = C ) } $.
    cncfres.4 $e |- G = { <. x , y >. | ( x e. A /\ y = C ) } $.
    cncfres.5 $e |- ( x e. A -> C e. B ) $.
    cncfres.6 $e |- F e. ( CC -cn-> CC ) $.
    cncfres.7 $e |- J = ( MetOpen ` ( ( abs o. - ) |` ( A X. A ) ) ) $.
    cncfres.8 $e |- K = ( MetOpen ` ( ( abs o. - ) |` ( B X. B ) ) ) $.
    $( A continuous function on complex numbers restricted to a subset.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    cncfres $p |- G e. ( J Cn K ) $=
      ( cc wcel wceq vz vw ccncf co ccn cres cv copab wss resopab2 ax-mp reseq1
      wa 3eqtr4ri wi ssid rescncf mp3an eqeltri w3a cfv wral 3pm3.2i csb visset
      ax-17 hbcsb1 hbel hbim eleq1 csbeq1a eleq1d imbi12d chvar fvopab4gf mpdan
      eqeltrd rgen cncffvrn mp2an cabs cmin ccom cxp eqid cncfmet eleqtri ) GCD
      UCUDZHIUEUDZGCRUCUDZSZGWHSZGFCUFZWJAUGZRSBUGETZUMABUHZCUFZWNCSZWOUMABUHZW
      MGCRUIZWQWSTJWOABCRUJUKFWPTWMWQTLFWPCULUKMUNFRRUCUDSZWMWJSZORRUIZXCWTXAXB
      UORUPZXDJRRCFUQURUKUSWTXCDRUIZUTUAUGZGVAZDSZUACVBWKWLUOWTXCXEJXDKVCXHUACX
      FCSZXGAXFEVDZDXIXJDSZXGXJTWREDSZUOXIXKUOAUAXIXKAXIAVFAUBUBXJDAUBXFEUAVEUB
      UGZXFSAVFZVGZXMDSAVFVHVIWNXFTZWRXIXLXKWNXFCVJXPEXJDAXFEVKZVLVMNVNZABUBXFE
      XJCDGXNXOXQMVOVPXRVQVRUACRDGVSVTUKWTXEWHWITJKCDWAWBWCZCCWDUFZXSDDWDUFZHIX
      TWEYAWEPQWFVTWG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    ishomeo2.1 $e |- X = U. J $.
    ishomeo2.2 $e |- Y = U. K $.
    $( The predicate "is a homeomorphism".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    ishomeo2 $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Homeo K ) <->
                          ( F : X -1-1-onto-> Y /\ A. x e. J ( F " x ) e. K /\
                                        A. x e. K ( `' F " x ) e. J ) ) ) $=
      ( ctop wcel wa chomeosm co cima wral w3a wi ishomeo 3expia cvv cv pm2.43d
      wf1o ccnv biimpd wf f1of cuni uniexg syl5eqel fex syl2an 3ad2antl1 expcom
      adantr biimprd com23 mpdd impbid ) CIJZDIJZKZBCDLMZJZEFBUCZBAUAZNDJACOZBU
      DVFNCJADOZPZVBVDVIUTVAVDVDVIQUTVAVDPVDVIAVCBCDEFGHRUESUBVBVIBTJZVDUTVIVJQ
      VAVIUTVJVEVGUTVJVHVEEFBUFETJVJUTEFBUGUTECUHTGCIUIUJEFTBUKULUMUNUOVBVJVIVD
      UTVAVJVIVDQUTVAVJPVDVIATBCDEFGHRUPSUQURUS $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J v $.  $d K v $.  $d F v $.
    $( A homeomorphism is continuous.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    hmeocn $p |- ( ( J e. Top /\ K e. Top ) ->
                            ( F e. ( J Homeo K ) -> F e. ( J Cn K ) ) ) $=
      ( vv ctop wcel wa cuni wf1o cv cima wral ccnv w3a wf chomeosm co ccn eqid
      wi f1of anim1i 3adant2 a1i ishomeo2 iscn 3imtr4d ) BEFCEFGZBHZCHZAIZADJZK
      CFDBLZAMULKBFDCLZNZUIUJAOZUNGZABCPQFABCRQFUOUQTUHUKUNUQUMUKUPUNUIUJAUAUBU
      CUDDABCUIUJUISZUJSZUEDABCUIUJURUSUFUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $( The inverse of a homeomorphism is a homeomorphism.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    hmeocnv $p |- ( ( ( J e. Top /\ K e. Top ) /\ F e. ( J Homeo K ) )
                                            -> `' F e. ( K Homeo J ) ) $=
      ( ctop wcel wa chomeosm co ccnv simpr cuni wf1o ccn adantl wb eqid hmeobc
      w3a 3expa cvv f1ocnv 3ad2ant1 simpr3 wrel wceq f1orel dfrel2 sylib eleq1d
      wi biimpar 3adant3 3jca ex adantr cnvexg 3com12 sylan2 3imtr4d mpd ) BDEZ
      CDEZFZABCGHZEZFZVEAIZCBGHEZVCVEJVFBKZCKZALZABCMHZEZVGCBMHEZRZVJVIVGLZVNVG
      IZVLEZRZVEVHVCVOVSUJVEVCVOVSVCVOFVPVNVRVOVPVCVKVMVPVNVIVJAUAUBNVCVKVMVNUC
      VOVRVCVKVMVRVNVKVRVMVKVQAVLVKAUDVQAUEVIVJAUFAUGUHUIUKULNUMUNUOVAVBVEVEVOO
      VDABCVIVJVIPZVJPZQSVEVCVGTEZVHVSOZAVDUPVAVBWBWCVBVAWBWCTVGCBVJVIWAVTQUQSU
      RUSUT $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X x y $.  $d J x y $.  $d K x y $.  $d A x y $.  $d F x y $.
    hmeoopn.1 $e |- X = U. J $.
    $( Homeomorphisms preserve openness.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    hmeoopn $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( F e. ( J Homeo K ) /\
                              A C_ X ) ) -> ( A e. J <-> ( F " A ) e. K ) ) $=
      ( vx vy ctop wcel wa cima wi cv wral imp wceq imaeq2 eleq1d rcla4v co wss
      chomeosm hmeocnb syl5com adantrr ccnv hmeocna cuni wf1 wf1o hmeomap f1of1
      eqid syl f1imacnv sylan anasss sylibd impbid ) CIJDIJKZBCDUCUAJZAEUBZKKZA
      CJZBALZDJZVAVBVEVGMVCVAVBKZBGNZLZDJZGCOZVEVGVAVBVLGBCDUDPVKVGGACVIAQVJVFD
      VIABRSTUEUFVDVGBUGZVFLZCJZVEVAVBVGVOMVCVHVMHNZLZCJZHDOZVGVOVAVBVSHBCDUHPV
      RVOHVFDVPVFQVQVNCVPVFVMRSTUEUFVDVNACVAVBVCVNAQZVHEDUIZBUJZVCVTVHEWABUKZWB
      VAVBWCBCDEWAFWAUNULPEWABUMUOEWAABUPUQURSUSUT $.
      $( [2-Sep-2009] $)
  $}

  ${
    hmeocld.1 $e |- X = U. J $.
    $( Homeomorphisms preserve closedness.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    hmeocld $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( F e. ( J Homeo K ) /\
        A C_ X ) ) -> ( A e. ( Clsd ` J ) <-> ( F " A ) e. ( Clsd ` K ) ) ) $=
      ( ctop wcel wa chomeosm wss cdif cima ccld cfv wb adantrr wceq syl iscld2
      co cuni hmeoopn mpanr2 ccnv wfun wf1o eqid hmeomap imp wfo dff1o3 simprbi
      difss imadif f1ofo foima 3syl difeq1d eqtrd eleq1d bitrd ad2ant2rl simplr
      crn imassrn forn syl5sseq adantr sylan anasss syl11anc 3bitr4d ) CGHZDGHZ
      IZBCDJUAHZAEKZIZIZEALZCHZDUBZBAMZLZDHZACNOHZWDDNOHZVTWBBWAMZDHZWFVPVQWBWJ
      PZVRVPVQWAEKWKEAUNWABCDEFUCUDQVTWIWEDVTWIBEMZWDLZWEVTBUEUFZWIWMRVPVQWNVRV
      PVQIZEWCBUGZWNVPVQWPBCDEWCFWCUHZUIUJZWPEWCBUKZWNEWCBULUMSQEABUOSVTWLWCWDV
      PVQWLWCRZVRWOWPWSWTWREWCBUPZEWCBUQURQUSUTVAVBVNVRWGWBPVOVQACEFTVCVTVOWDWC
      KZWHWFPVNVOVSVDVPVQVRXBWOWPVRXBWRWPXBVRWPBVEZWDWCBAVFWPWSXCWCRXAEWCBVGSVH
      VIVJVKWDDWCWQTVLVM $.
      $( [2-Sep-2009] $)
  $}

