$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Continuous maps and homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.
    constcncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = A ) } $.
    $( A constant function is a continuous function on ` CC ` .  (Contributed
       by Jeff Madsen, 2-Sep-2009.) $)
    constcncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( cc wcel cabs cmin ccom copn cfv ccn co ccncf cme csn wf eqid cv a1i cxp
      cnmet id fconstg wceq wa copab fconstopab eqtr4i feq1i cnmetba metcnconst
      sylibr syl22anc cncfmet1 syl6eleqr ) CFGZDHIJZKLZUTMNZFFONURUSPGZVBURFCQZ
      DRZDVAGVBURUSUSSZUCUAZVFURUDURFVCFVCUBZRVDFCFUEFVCDVGDATFGBTCUFUGABUHVGEA
      BFCUIUJUKUNCUSUSDUTUTFFUSVEULZVHUTSZVIUMUOUSUTVEVIUPUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y $.  $d F u v w $.
    addccncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( x + A ) ) } $.
    $( Adding a constant is a continuous function.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    addccncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vv vw cc wcel co cv caddc ancoms wa cfv cmin cabs clt wceq wss ccncf
      ssid wf ax-addcl ralrimiva fopab2 sylib crp wi simpr a1i wbr opreq1 oprex
      wral fvopab4 ad2antrl ad2antll opreq12d pnpcan2 3expa eqtrd fveq2d breq1d
      adantrr exbiri elcncf1di mp2ani ) CIJZIIUAZVKDIIUBKJIUCZVLVJFGHIIDGLZVJAL
      ZCMKZIJZAIUPIIDUDVJVPAIVNIJVJVPVNCUENUFABIIVODEUGUHFLZIJZVMUIJZOVSUJVJVRV
      SUKULVJVRHLZIJZOZVSOZVQDPZVTDPZQKZRPZVMSUMVQVTQKZRPZVMSUMVJWCOZWGWIVMSWJW
      FWHRVJWBWFWHTVSVJWBOZWFVQCMKZVTCMKZQKZWHWKWDWLWEWMQVRWDWLTVJWAABVQVOWLIDV
      NVQCMUNEVQCMUOUQURWAWEWMTVJVRABVTVOWMIDVNVTCMUNEVTCMUOUQUSUTWBVJWNWHTZVRW
      AVJWOVQVTCVAVBNVCVFVDVEVGVHVI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y $.
    idcncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = x ) } $.
    $( The identity function is a continuous function on ` CC ` .  (Contributed
       by Jeff Madsen, 11-Jun-2010.) $)
    idcncf $p |- F e. ( CC -cn-> CC ) $=
      ( cc0 cc wcel ccncf co 0cn cv wa copab caddc addid1 eqcomd eqeq2d pm5.32i
      wceq opabbii eqtri addccncf ax-mp ) EFGCFFHIGJABECCAKZFGZBKZUDSZLZABMUEUF
      UDENIZSZLZABMDUHUKABUEUGUJUEUDUIUFUEUIUDUDOPQRTUAUBUC $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d A u v w x y $.  $d F u v w $.
    sub1cncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( x - A ) ) } $.
    $( Subtracting a constant is a continuous function.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    sub1cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vw vv cc wcel co cv cmin ancoms wa cfv cabs clt wbr wceq wss ssid wf
      ccncf wral subcl ralrimiva fopab2 sylib crp wi simpr opreq1 oprex fvopab4
      a1i ad2antrl ad2antll opreq12d nnncan2 3expa fveq2d adantrr breq1d exbiri
      eqtrd elcncf1di mp2ani ) CIJZIIUAZVJDIIUDKJIUBZVKVIFGHIIDGLZVIALZCMKZIJZA
      IUEIIDUCVIVOAIVMIJVIVOVMCUFNUGABIIVNDEUHUIFLZIJZVLUJJZOVRUKVIVQVRULUPVIVQ
      HLZIJZOZVROZVPDPZVSDPZMKZQPZVLRSVPVSMKZQPZVLRSVIWBOWFWHVLRVIWAWFWHTVRVIWA
      OZWEWGQWIWEVPCMKZVSCMKZMKZWGWIWCWJWDWKMVQWCWJTVIVTABVPVNWJIDVMVPCMUMEVPCM
      UNUOUQVTWDWKTVIVQABVSVNWKIDVMVSCMUMEVSCMUNUOURUSWAVIWLWGTZVQVTVIWMVPVSCUT
      VANVFVBVCVDVEVGVH $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y $.  $d F u v w $.
    sub2cncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( A - x ) ) } $.
    $( Subtraction from a constant is a continuous function.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    sub2cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vw vv cc wcel co cv cmin wa cfv cabs clt wbr wceq opreq2 wss ssid wf
      ccncf wral subcl ralrimiva fopab2 sylib crp wi simpr a1i fvopab4 ad2antrl
      oprex ad2antll opreq12d nnncan1 3expb fveq2d abssub ancoms adantl adantrr
      eqtrd breq1d exbiri elcncf1di mp2ani ) CIJZIIUAZVLDIIUDKJIUBZVMVKFGHIIDGL
      ZVKCALZMKZIJZAIUEIIDUCVKVQAICVOUFUGABIIVPDEUHUIFLZIJZVNUJJZNVTUKVKVSVTULU
      MVKVSHLZIJZNZVTNZVRDOZWADOZMKZPOZVNQRVRWAMKPOZVNQRVKWDNWHWIVNQVKWCWHWISVT
      VKWCNZWHWAVRMKZPOZWIWJWGWKPWJWGCVRMKZCWAMKZMKZWKWJWEWMWFWNMVSWEWMSVKWBABV
      RVPWMIDVOVRCMTECVRMUPUNUOWBWFWNSVKVSABWAVPWNIDVOWACMTECWAMUPUNUQURVKVSWBW
      OWKSCVRWAUSUTVFVAWCWLWISZVKWBVSWPWAVRVBVCVDVFVEVGVHVIVJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d J x y z $.  $d K x y z $.  $d F x y z $.  $d X x y z $.  $d Y x y z $.
    $d A x y z $.  $d B x y z $.
    cnres2.1 $e |- X = U. J $.
    cnres2.2 $e |- Y = U. K $.
    $( The restriction of a continuous function to a subset is continuous.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    cnres2 $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( A C_ X /\ B C_ Y ) /\
                ( F e. ( J Cn K ) /\ A. x e. A ( F ` x ) e. B ) ) -> ( F |` A )
                  e. ( ( subSp ` <. A , J >. ) Cn ( subSp ` <. B , K >. ) ) )
        $=
      ( ctop wcel wa wss ccn co cfv cop csubsp syl22anc cv wral w3a cres simp1l
      simp1r simp2l simp3l cnsubsp crn wb id topopn ssexg syl2anr stoig3 syldan
      cvv ad2ant2r 3adant3 cima df-ima simp3r wfun cdm wf cnf syl3anc ffun wceq
      syl fdm sseqtr4d funimass4 syl2anc mpbird syl5eqssr simp2r cnsubsp2 mpbid
      ) EKLZFKLZMZBGNZCHNZMZDEFOPLZAUADQCLABUBZMZUCZDBUDZBERSQZFOPLZWKWLCFRSQOP
      LZWJWAWBWDWGWMWAWBWFWIUEZWAWBWFWIUFZWCWDWEWIUGZWCWFWGWHUHZBDEFGIUITWJWLKL
      ZWBWKUJZCNWEWMWNUKWCWFWSWIWAWDWSWBWEWAWDBURLZWSWDWDGELXAWAWDULEGIUMBGEUNU
      OBEURUPUQUSUTWPWJWTDBVAZCDBVBWJXBCNZWHWCWFWGWHVCWJDVDZBDVEZNXCWHUKWJGHDVF
      ZXDWJWAWBWGXFWOWPWRDEFGHIJVGVHZGHDVIVKWJBGXEWQWJXFXEGVJXGGHDVLVKVMABCDVNV
      OVPVQWCWDWEWIVRCWKWLFHJVSTVT $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J x y z $.  $d K x y z $.  $d F x y z $.
    $( A continuous function is continuous onto its image.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    cnresima $p |- ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) )
                              -> F e. ( J Cn ( subSp ` <. ran F , K >. ) ) ) $=
      ( vx vy vz ctop wcel wa cuni wf cv cima eqid cvv wceq sylan2 syl eleq1d
      wb ccn co crn cop csubsp cfv ccnv wral biimpa simpll wss frn uniexg ssexg
      syl2anr stoig3 syldan ad2ant2lr syl2anc wfn ffn dffn3 sylib adantl stoig2
      iscn feq3 mpbid cin wrex fex ancoms rnexg elsubsp anassrs ancom1s adantrr
      wi imaeq2 rcla4cva fnfvelrn simpl pm3.21 impbid2 syl5bb pm5.32da elpreima
      elin 3bitr4d eqrdv biimpar syl5ibrcom rexlimdva anasss ralrimiv mpbir2and
      sylbid 3impa ) BGHZCGHZABCUAUBHZABAUCZCUDUEUFZUAUBHZWSWTIZXABJZCJZAKZAUGZ
      DLZMZBHZDCUHZIZXDXEXAXNDABCXFXGXFNZXGNVFUIXEXNIZXDXFXCJZAKZXIELZMZBHZEXCU
      HZXPWSXCGHZXDXRYBITWSWTXNUJWTXHYCWSXMWTXHXBOHZYCXHXBXGUKZXGOHYDWTXFXGAULZ
      CGUMXBXGOUNUOXBCOUPUQUREABXCXFXQXOXQNVFUSWTXHXRWSXMWTXHIZXFXBAKZXRXHYHWTX
      HAXFUTZYHXFXGAVAZXFAVBVCVDYGXBXQPZYHXRTXHWTYEYKYFXBCVEQXBXQXFAVGRVHURXPYA
      EXCXPXSXCHZXSFLZXBVIZPZFCVJZYAXEXHYLYPTZXMWTWSXHYQWTWSXHYQWSXHIZWTYDYQYRA
      OHZYDXHWSYSWSXHXFOHYSBGUMXFXGOAVKQVLAOVMRFXSXBCOVNQVOVPVQXEXHXMYPYAVRXEXH
      IZXMIYOYAFCYTXMYMCHZYOYAVRZXMUUAIYTXIYMMZBHZUUBXLUUDDYMCXJYMPXKUUCBXJYMXI
      VSSVTYTUUDIYAYOXIYNMZBHZYTUUFUUDYTUUEUUCBXHUUEUUCPZXEXHYIUUGYJYIDUUEUUCYI
      XJXFHZXJAUFZYNHZIUUHUUIYMHZIXJUUEHXJUUCHYIUUHUUJUUKUUJUUKUUIXBHZIZYIUUHIZ
      UUKUUIYMXBWHUUNUULUUMUUKTXFXJAWAUULUUMUUKUUKUULWBUULUUKWCWDRWEWFXFXJYNAWG
      XFXJYMAWGWIWJRVDSWKYOXTUUEBXSYNXIVSSWLQVOWMWNWQWOWPUQWR $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J u v $.  $d K u v $.  $d Y u v $.  $d A u v $.  $d F u v $.
    cnss.1 $e |- Y = U. K $.
    $( A continuous function into a subspace is continuous into the larger
       space.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    cnss $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( A C_ Y /\
              F e. ( J Cn ( subSp ` <. A , K >. ) ) ) ) -> F e. ( J Cn K ) ) $=
      ( vv vu ctop wcel wa wss cuni wf cima wceq wb cin adantr cvv cop cfv ccnv
      csubsp ccn co cv wral sseq2i stoig2 sylan2b adantll feq3 anbi1d wi expcom
      syl fss adantrd wfun ffun ad2antlr inpreima crn imassrn cdm dfdm4 syl5eqr
      adantl fdm syl5sseq fimacnv sseqtr4d df-ss sylib eqtr2d wrex ineq1 eqeq2d
      rcla4ev mpan2 uniexg syl5eqel ssexg sylan2 ancoms elsubsp syldan adantlll
      eqid mpbird imaeq2 rcla4va sylan an32s adantlrl eqeltrd ralrimiva ex jcad
      eleq1d sylbird id topopn syl2anr stoig3 iscn anassrs 3imtr4d impr ) CIJZD
      IJZKZAELZBCADUAUDUBZUEUFJZBCDUEUFJZXMXNKZCMZXOMZBNZBUCZGUGZOZCJZGXOUHZKZX
      SEBNZYBHUGZOZCJZHDUHZKZXPXQXRYGXSABNZYFKZYMXRYNYAYFXRAXTPZYNYAQXLXNYPXKXN
      XLADMZLYPEYQAFUIADUJUKULAXTXSBUMUQUNXRYOYHYLXNYOYHUOXMXNYNYHYFYNXNYHXSAEB
      URUPUSVIXRYOYLXRYOKZYKHDYRYIDJZKZYJYBYIARZOZCYTUUBYJYBAOZRZYJYTBUTZUUBUUD
      PYOUUEXRYSYNUUEYFXSABVASVBYIABVCUQYTYJUUCLZUUDYJPYOUUFXRYSYNUUFYFYNYJXSUU
      CYNYBVDZYJXSYBYIVEYNUUGBVFXSBVGXSABVJVHVKXSABVLVMSVBYJUUCVNVOVPXRYFYSUUBC
      JZYNXRYSYFUUHXRYSKUUAXOJZYFUUHXLXNYSUUIXKXLXNKZYSKUUIUUAYCARZPZGDVQZYSUUM
      UUJYSUUAUUAPZUUMUUAWJUULUUNGYIDYCYIPUUKUUAUUAYCYIAVRVSVTWAVIUUJUUIUUMQZYS
      XLXNATJZUUOXNXLUUPXLXNETJUUPXLEYQTFDIWBWCAETWDWEWFGUUAADTWGWHSWKWIYEUUHGU
      UAXOYCUUAPYDUUBCYCUUAYBWLXAWMWNWOWPWQWRWSWTXBXKXLXNXPYGQZUUJXKXOIJZUUQXLX
      NUUPUURXNXNEDJUUPXLXNXCDEFXDAEDWDXEADTXFWHGBCXOXSXTXSWJZXTWJXGWEXHXMXQYMQ
      XNHBCDXSEUUSFXGSXIXJ $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d J y $.  $d K y $.  $d A y $.  $d B y $.  $d X y $.  $d Y y $.  $d F y $.
    paste.1 $e |- X = U. J $.
    paste.2 $e |- Y = U. K $.
    $( Pasting lemma.  If ` A ` and ` B ` are closed sets in ` X ` with
       ` A u. B = X ` , then any function whose restrictions to ` A ` and ` B `
       are continuous is continuous on all of ` X ` .  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    paste $p |- ( ( ( J e. Top /\ K e. Top ) /\
            ( A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) /\ ( A u. B ) = X ) /\
              ( F : X --> Y /\ ( F |` A ) e. ( ( subSp ` <. A , J >. ) Cn K )
                      /\ ( F |` B ) e. ( ( subSp ` <. B , J >. ) Cn K ) ) )
                                          -> F e. ( J Cn K ) ) $=
      ( vy ctop wcel wa ccld cfv wceq w3a cin adantr wi cun cres cop csubsp ccn
      wf co ccnv cv cima wral wb iscncl 3ad2ant1 simp1 3ad2ant3 wss crn imassrn
      cdm dfdm4 fdm syl5eqr syl5sseq df-ss biimpi eqcomd syl adantl eqcoms wfun
      ineq2 ffun respreima uneq12d syl6reqr 3ad2antr1 3ad2antl3 3adant1 simpl1l
      indi 3eqtrd stoig3 cnclima 3expia sylan an32s subspcld2 adantlr 3ad2antr2
      ex imp syld 3impia 3ad2antr3 uncld syl3anc eqeltrd ralrimiva mpbir2and )
      DKLZEKLZMZADNOZLZBXDLZABUAZFPZQZFGCUFZCAUBZADUCUDOZEUEUGLZCBUBZBDUCUDOZEU
      EUGLZQZQZCDEUEUGLZXJCUHZJUIZUJZXDLZJENOZUKZXCXIXSXJYEMULXQJCDEFGHIUMUNXQX
      CXJXIXJXMXPUOUPXRYCJYDXRYAYDLZMZYBXKUHYAUJZXNUHYAUJZUAZXDXRYBYJPZYFXIXQYK
      XCXHXEXQYKXFXHXMXJYKXPXHXJMYBYBFRZYBXGRZYJXJYBYLPZXHXJYBFUQZYNXJXTURZYBFX
      TYAUSXJYPCUTFCVAFGCVBVCVDYOYLYBYOYLYBPYBFVEVFVGVHVIXHYLYMPZXJYQFXGFXGYBVL
      VJSXJYMYJPXHXJYJYBARZYBBRZUAZYMXJCVKZYJYTPFGCVMUUAYHYRYIYSYAACVNYABCVNVOV
      HYBABWAVPVIWBVQVRVSSYGXAYHXDLZYIXDLZYJXDLXAXBXIXQYFVTXRYFUUBXCXIXQYFUUBTZ
      XCXFXEXQUUDTXHXCXEMZXQUUDUUEXJXMUUDXPUUEXMMYFYHXLNOLZUUBUUEXMYFUUFTZXAXEX
      BXMUUGTZXAXEMXLKLZXBUUHADXDWCUUIXBXMUUGUUIXBXMQYFUUFYAXKXLEWDWKWEWFWGWLUU
      EUUFUUBTZXMXAXEUUJXBXAXEUUFUUBAYHDWHWEWISWMWJWKVQWNWLXRYFUUCXCXIXQYFUUCTZ
      XCXEXFXQUUKTXHXCXFMZXQUUKUULXJXPUUKXMUULXPMYFYIXONOLZUUCUULXPYFUUMTZXAXFX
      BXPUUNTZXAXFMXOKLZXBUUOBDXDWCUUPXBXPUUNUUPXBXPQYFUUMYAXNXOEWDWKWEWFWGWLUU
      LUUMUUCTZXPXAXFUUQXBXAXFUUMUUCBYIDWHWEWISWMWOWKWJWNWLYHYIDWPWQWRWSWT $.
      $( [15-Dec-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y K $.  $d x y ph $.
    $d x y X $.
    cnmpt2pc.r $e |- R = ( topGen ` ran (,) ) $.
    cnmpt2pc.m $e |- M = ( subSp ` <. ( A [,] B ) , R >. ) $.
    cnmpt2pc.n $e |- N = ( subSp ` <. ( B [,] C ) , R >. ) $.
    cnmpt2pc.o $e |- O = ( subSp ` <. ( A [,] C ) , R >. ) $.
    cnmpt2pc.x $e |- X = U. J $.
    cnmpt2pc.a $e |- ( ph -> A e. RR ) $.
    cnmpt2pc.c $e |- ( ph -> C e. RR ) $.
    cnmpt2pc.b $e |- ( ph -> B e. ( A [,] C ) ) $.
    cnmpt2pc.j $e |- ( ph -> J e. Top ) $.
    cnmpt2pc.k $e |- ( ph -> K e. Top ) $.
    cnmpt2pc.q $e |- ( ( ph /\ ( x = B /\ y e. X ) ) -> D = E ) $.
    cnmpt2pc.d $e |- ( ph -> ( x e. ( A [,] B ) , y e. X |-> D )
                               e. ( ( M tX J ) Cn K ) ) $.
    cnmpt2pc.e $e |- ( ph -> ( x e. ( B [,] C ) , y e. X |-> E )
                               e. ( ( N tX J ) Cn K ) ) $.
    $( Piecewise definition of a continuous function on a real interval.
       (Contributed by Jeff Madsen, 2-Sep-2009.)  (Revised by Mario Carneiro,
       5-Jun-2014.) $)
    cnmpt2pc $p |- ( ph -> ( x e. ( A [,] C ) , y e. X |->
                            if ( x <_ B , D , E ) ) e. ( ( O tX J ) Cn K ) ) $=
      ( ctx co ctop wcel cicc cxp ccld cfv cun cuni wceq cle wbr cif cmpt2 cres
      cv cop csubsp ccn cvv cioo crn ctg retop eqeltri oprex stoig3 mp2an txtop
      wf a1i syl2anc cr iccssre sseldd icccld fveq2i syl6eleqr iccsplit syl3anc
      wss ssun1 syl5sseqr uniretop unieqi eqtr4i subspcld syl22anc topcld txcld
      syl ssun2 xpeq1d xpundir stoig2 syl6eqr xpeq12d eqid txuni eqtr2d wral wa
      w3a elicc2 biimpa 3adant3 mpt2eq3dva eqeltrd cnf sstrd feq2d mpbid sylibr
      wb fmpt2 mpbird sylib resmpt2 sylancl txsubsp subspco df-ss opeq1d fveq2d
      cin eqtrd opeq2i 3eqtr4g opreq12d opreq1d eleqtrrd syl6sseq eqtr4d simp3d
      syl6req iftrue simp2d biantrud simp1d adantr letri3 bitr4d ancom2s ifeq1d
      wi ifid syl6eq expr 3adant2 sylbid iffalse pm2.61d1 ralun raleqdv subspid
      ssid paste syl233anc ) ANJUIUJZUKULZKUKULZDEUMUJZOUNZUVHUOUPZULZEFUMUJZOU
      NZUVMULZUVLUVPUQZUVHURZUSUVSKURZBCDFUMUJZOBVEZEUTVAZGIVBZVCZVSZUWEUVLVDZU
      VLUVHVFVGUPZKVHUJZULUWEUVPVDZUVPUVHVFVGUPZKVHUJZULUWEUVHKVHUJULANUKULZJUK
      ULZUVIUWMANUWAHVFVGUPZUKSHUKULZUWAVIULZUWOUKULHVJVKVLUPZUKPVMVNZDFUMVOZUW
      AHVIVPVQVNVTZUDNJVRWAUEAUWMUWNUVKNUOUPZULOJUOUPZULZUVNUXAUDAUVKUWOUOUPZUX
      BAUWPUWAWBWJZUVKHUOUPZULUVKUWAWJZUVKUXEULUWPAUWSVTZADWBULZFWBULZUXFUAUBDF
      WCWAZAUVKUWRUOUPZUXGAUXJEWBULZUVKUXMULUAAUWAWBEUXLUCWDZDEWEWAHUWRUOPWFZWG
      AUVKUVOUQZUVKUWAUVKUVOWKAUXJUXKEUWAULUWAUXQUSUAUBUCDFEWHWIZWLZUWAUVKHWBWB
      UWRURHURZWMHUWRPWNWOZWPWQNUWOUOSWFZWGAUWNUXDUDJOTWRWTZUVKONJWSWQAUWMUWNUV
      OUXBULUXDUVQUXAUDAUVOUXEUXBAUWPUXFUVOUXGULUVOUWAWJZUVOUXEULUXIUXLAUVOUXMU
      XGAUXNUXKUVOUXMULUXOUBEFWEWAUXPWGAUXQUVOUWAUVOUVKXAUXRWLZUWAUVOHWBUYAWPWQ
      UYBWGUYCUVOONJWSWQAUVRUWAOUNZUVSAUYFUXQOUNUVRAUWAUXQOUXRXBUVKUVOOXCUUDAUY
      FNURZJURZUNZUVSAUWAUYGOUYHAUWAUWOURZUYGAUWPUWAUXTWJUWAUYJUSUXIAUWAWBUXTUX
      LUYAUUAZUWAHXDWANUWOSWNXEOUYHUSATVTZXFAUWMUWNUYIUVSUSUXAUDNJUYGUYHUYGXGUY
      HXGZXHWAXIZUUBAUWFUYFUVTUWEVSZAUWDUVTULCOXJZBUWAXJZUYOAUYQUYPBUXQXJZAUYPB
      UVKXJZUYPBUVOXJZUYRAUVLUVTBCUVKOUWDVCZVSZUYSALJUIUJZURZUVTVUAVSZVUBAVUCUK
      ULZUVJVUAVUCKVHUJZULVUEALUKULZUWNVUFVUHALUVKHVFZVGUPZUKQUWPUVKVIULZVUJUKU
      LUWSDEUMVOZUVKHVIVPVQVNVTZUDLJVRWAUEAVUABCUVKOGVCVUGABCUVKOUWDGAUWBUVKULZ
      CVEOULZXLUWCUWDGUSAVUNUWCVUOAVUNXKUWBWBULZDUWBUTVAZUWCAVUNVUPVUQUWCXLZAUX
      JUXNVUNVURYCUAUXODEUWBXMWAXNUUCXOUWCGIUUEWTXPUGXQZVUAVUCKVUDUVTVUDXGUVTXG
      ZXRWIAVUDUVLUVTVUAAUVLLURZUYHUNZVUDAUVKVVAOUYHAUVKVUJURZVVAAUWPUVKUXTWJUV
      KVVCUSUXIAUVKUWAUXTUXSUYKXSUVKHXDWALVUJQWNXEUYLXFAVUHUWNVVBVUDUSVUMUDLJVV
      AUYHVVAXGUYMXHWAXIXTYABCUVKOUWDUVTVUAVUAXGYDYBAUVPUVTBCUVOOUWDVCZVSZUYTAM
      JUIUJZURZUVTVVDVSZVVEAVVFUKULZUVJVVDVVFKVHUJZULVVHAMUKULZUWNVVIVVKAMUVOHV
      FZVGUPZUKRUWPUVOVIULZVVMUKULUWSEFUMVOZUVOHVIVPVQVNVTZUDMJVRWAUEAVVDBCUVOO
      IVCVVJABCUVOOUWDIAUWBUVOULZVUOXLZUWCUWDIUSZVVRUWCUWBEUSZVVSAVVQUWCVVTYCVU
      OAVVQXKZUWCUWCEUWBUTVAZXKZVVTVWAVWBUWCVWAVUPVWBUWBFUTVAZAVVQVUPVWBVWDXLZA
      UXNUXKVVQVWEYCUXOUBEFUWBXMWAXNZUUFUUGVWAVUPUXNVVTVWCYCVWAVUPVWBVWDVWFUUHA
      UXNVVQUXOUUIUWBEUUJWAUUKXOAVUOVVTVVSUUNVVQAVUOVVTVVSAVUOVVTXKXKZUWDUWCIIV
      BIVWGUWCGIIAVVTVUOGIUSUFUULUUMUWCIUUOUUPUUQUURUUSUWCGIUUTUVAXPUHXQZVVDVVF
      KVVGUVTVVGXGVUTXRWIAVVGUVPUVTVVDAUVPMURZUYHUNZVVGAUVOVWIOUYHAUVOVVMURZVWI
      AUWPUVOUXTWJUVOVWKUSUXIAUVOUWAUXTUYEUYKXSUVOHXDWAMVVMRWNXEUYLXFAVVKUWNVWJ
      VVGUSVVPUDMJVWIUYHVWIXGUYMXHWAXIXTYABCUVOOUWDUVTVVDVVDXGYDYBUYPBUVKUVOUVB
      WAAUYPBUWAUXQUXRUVCYEBCUWAOUWDUVTUWEUWEXGYDYFAUVSUYFUVTUWEUYNXTYEAUWGVUGU
      WIAUWGVUAVUGAUXHOOWJZUWGVUAUSUXSOUVEZBCUWAOUVKOUWDYGYHVUSXQAUWHVUCKVHAUWH
      UVKNVFZVGUPZOJVFVGUPZUIUJZVUCAUWMUWNVUKUXDUWHVWQUSUXAUDVUKAVULVTZUYCUVKON
      JVIUXCYIWQAVWOLVWPJUIAUVKUWOVFZVGUPZVUJVWOLAVWTUVKUWAYNZHVFZVGUPZVUJAUWPV
      UKUWQVWTVXCUSUXIVWRUWQAUWTVTZUVKUWAHVIVIYJWIAVXBVUIVGAVXAUVKHAUXHVXAUVKUS
      UXSUVKUWAYKYFYLYMYOVWNVWSVGNUWOUVKSYPWFQYQAUWNVWPJUSUDJOTUVDWTZYRYOYSYTAU
      WJVVJUWLAUWJVVDVVJAUYDVWLUWJVVDUSUYEVWMBCUWAOUVOOUWDYGYHVWHXQAUWKVVFKVHAU
      WKUVONVFZVGUPZVWPUIUJZVVFAUWMUWNVVNUXDUWKVXHUSUXAUDVVNAVVOVTZUYCUVOONJVIU
      XCYIWQAVXGMVWPJUIAUVOUWOVFZVGUPZVVMVXGMAVXKUVOUWAYNZHVFZVGUPZVVMAUWPVVNUW
      QVXKVXNUSUXIVXIVXDUVOUWAHVIVIYJWIAVXMVVLVGAVXLUVOHAUYDVXLUVOUSUYEUVOUWAYK
      YFYLYMYOVXFVXJVGNUWOUVOSYPWFRYQVXEYRYOYSYTUVLUVPUWEUVHKUVSUVTUVSXGVUTUVFU
      VG $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d A w x y z $.  $d B w x y z $.  $d C w y z $.  $d J x y $.  $d K x y $.
    $d G z $.
    cncfres.1 $e |- A C_ CC $.
    cncfres.2 $e |- B C_ CC $.
    cncfres.3 $e |- F = { <. x , y >. | ( x e. CC /\ y = C ) } $.
    cncfres.4 $e |- G = { <. x , y >. | ( x e. A /\ y = C ) } $.
    cncfres.5 $e |- ( x e. A -> C e. B ) $.
    cncfres.6 $e |- F e. ( CC -cn-> CC ) $.
    cncfres.7 $e |- J = ( MetOpen ` ( ( abs o. - ) |` ( A X. A ) ) ) $.
    cncfres.8 $e |- K = ( MetOpen ` ( ( abs o. - ) |` ( B X. B ) ) ) $.
    $( A continuous function on complex numbers restricted to a subset.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    cncfres $p |- G e. ( J Cn K ) $=
      ( cc wcel wceq vz vw ccncf co ccn cres cv copab wss resopab2 ax-mp reseq1
      wa 3eqtr4ri ssid rescncf mp3an eqeltri w3a cfv wral 3pm3.2i csb ax-17 vex
      wi hbcsb1 hbel eleq1 csbeq1a eleq1d imbi12d chvar fvopab4gf mpdan eqeltrd
      hbim rgen cncffvrn mp2an cabs cmin ccom cxp eqid cncfmet eleqtri ) GCDUCU
      DZHIUEUDZGCRUCUDZSZGWHSZGFCUFZWJAUGZRSBUGETZUMABUHZCUFZWNCSZWOUMABUHZWMGC
      RUIZWQWSTJWOABCRUJUKFWPTWMWQTLFWPCULUKMUNFRRUCUDSZWMWJSZORRUIZXCWTXAXBVFR
      UOZXDJRRCFUPUQUKURWTXCDRUIZUSUAUGZGUTZDSZUACVAWKWLVFWTXCXEJXDKVBXHUACXFCS
      ZXGAXFEVCZDXIXJDSZXGXJTWREDSZVFXIXKVFAUAXIXKAXIAVDAUBUBXJDAUBXFEUAVEUBUGZ
      XFSAVDZVGZXMDSAVDVHVQWNXFTZWRXIXLXKWNXFCVIXPEXJDAXFEVJZVKVLNVMZABUBXFEXJC
      DGXNXOXQMVNVOXRVPVRUACRDGVSVTUKWTXEWHWITJKCDWAWBWCZCCWDUFZXSDDWDUFZHIXTWE
      YAWEPQWFVTWG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X x y $.  $d J x y $.  $d K x y $.  $d A x y $.  $d F x y $.
    hmeoopn.1 $e |- X = U. J $.
    $( Homeomorphisms preserve openness.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    hmeoopn $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( F e. ( J Homeo K ) /\
                              A C_ X ) ) -> ( A e. J <-> ( F " A ) e. K ) ) $=
      ( vx vy ctop wcel wa cima wi cv wral imp wceq imaeq2 eleq1d rcla4v co wss
      chomeosm hmeocnb syl5com adantrr ccnv hmeocna cuni wf1 wf1o hmeomap f1of1
      eqid syl f1imacnv sylan anasss sylibd impbid ) CIJDIJKZBCDUCUAJZAEUBZKKZA
      CJZBALZDJZVAVBVEVGMVCVAVBKZBGNZLZDJZGCOZVEVGVAVBVLGBCDUDPVKVGGACVIAQVJVFD
      VIABRSTUEUFVDVGBUGZVFLZCJZVEVAVBVGVOMVCVHVMHNZLZCJZHDOZVGVOVAVBVSHBCDUHPV
      RVOHVFDVPVFQVQVNCVPVFVMRSTUEUFVDVNACVAVBVCVNAQZVHEDUIZBUJZVCVTVHEWABUKZWB
      VAVBWCBCDEWAFWAUNULPEWABUMUOEWAABUPUQURSUSUT $.
      $( [2-Sep-2009] $)
  $}

  ${
    hmeocld.1 $e |- X = U. J $.
    $( Homeomorphisms preserve closedness.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    hmeocld $p |- ( ( ( J e. Top /\ K e. Top ) /\ ( F e. ( J Homeo K ) /\
        A C_ X ) ) -> ( A e. ( Clsd ` J ) <-> ( F " A ) e. ( Clsd ` K ) ) ) $=
      ( ctop wcel wa chomeosm wss cdif cima ccld cfv wb adantrr wceq syl iscld2
      co cuni hmeoopn mpanr2 ccnv wfun wf1o eqid hmeomap imp wfo dff1o3 simprbi
      difss imadif f1ofo foima 3syl difeq1d eqtrd eleq1d bitrd ad2ant2rl simplr
      crn imassrn forn syl5sseq adantr sylan anasss syl2anc 3bitr4d ) CGHZDGHZI
      ZBCDJUAHZAEKZIZIZEALZCHZDUBZBAMZLZDHZACNOHZWDDNOHZVTWBBWAMZDHZWFVPVQWBWJP
      ZVRVPVQWAEKWKEAUNWABCDEFUCUDQVTWIWEDVTWIBEMZWDLZWEVTBUEUFZWIWMRVPVQWNVRVP
      VQIZEWCBUGZWNVPVQWPBCDEWCFWCUHZUIUJZWPEWCBUKZWNEWCBULUMSQEABUOSVTWLWCWDVP
      VQWLWCRZVRWOWPWSWTWREWCBUPZEWCBUQURQUSUTVAVBVNVRWGWBPVOVQACEFTVCVTVOWDWCK
      ZWHWFPVNVOVSVDVPVQVRXBWOWPVRXBWRWPXBVRWPBVEZWDWCBAVFWPWSXCWCRXAEWCBVGSVHV
      IVJVKWDDWCWQTVLVM $.
      $( [2-Sep-2009] $)
  $}

