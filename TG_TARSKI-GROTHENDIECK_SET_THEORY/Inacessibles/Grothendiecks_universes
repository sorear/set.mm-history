$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Grothendieck's universes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Univ $. $( The class of all Grothendieck's universes. $)

  $( Extend class notation to include the class of all Grothendieck's
     universes. $)
  cgru $a class Univ $.

  ${
    $d u x y $.
    $( A Grothendieck's universe is a set that is closed with respect to all
       the operations that are common in set theory: pairs, powersets, unions,
       intersections, cross products etc.  Grothendieck and alii,
       S&eacute;minaire de G&eacute;om&eacute;trie Alg&eacute;brique 4,
       Expos&eacute; I, p. 185.  It was designed to give a precise meaning to
       the concepts of categories of sets, groups... $)
    df-gru $a |- Univ = { u | ( Tr u /\ A. x e. u ( ~P x e. u /\
      A. y e. u { x , y } e. u /\ A. y e. ( u ^m x ) U. ran y e. u ) ) } $.
  $}

  ${
    $d U u x y $.
    $( Properties of a Grothendieck's universe. $)
    elgrug $p |- ( U e. V ->
      ( U e. Univ <-> ( Tr U /\ A. x e. U ( ~P x e. U /\
        A. y e. U { x , y } e. U /\ A. y e. ( U ^m x ) U. ran y e. U ) ) ) ) $=
      ( vu cv wtr cpw wcel cpr wral crn cuni cmap co w3a cgru eleq2 raleqbi1dv
      wa wceq treq oveq1 raleqbidv 3anbi123d anbi12d df-gru elab2g ) EFZGZAFZHZ
      UIIZUKBFZJZUIIZBUIKZUNLMZUIIZBUIUKNOZKZPZAUIKZTCGZULCIZUOCIZBCKZURCIZBCUK
      NOZKZPZACKZTECQDUICUAZUJVDVCVLUICUBVBVKAUICVMUMVEUQVGVAVJUICULRUPVFBUICUI
      CUORSVMUSVHBUTVIUICUKNUCUICURRUDUESUFABEUGUH $.
      $( [9-Jun-2013] $)
  $}

  ${
    $d U x y z $.  $d A u x y z $.  $d B y $.  $d F x y z $.
    $( A Grothendieck's universe is transitive, so each element is a subset of
       the universe.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grutr $p |- ( ( U e. Univ /\ A e. U ) -> A C_ U ) $=
      ( vx vy cgru wcel wtr wss cv cpw cpr wral crn cuni cmap co w3a elgrug ibi
      wa simpld trss imp sylan ) BEFZBGZABFZABHZUEUFCIZJBFUIDIZKBFDBLUJMNBFDBUI
      OPLQCBLZUEUFUKTCDBERSUAUFUGUHBAUBUCUD $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains the powerset of each of its members.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grupw $p |- ( ( U e. Univ /\ A e. U ) -> ~P A e. U ) $=
      ( vy vx cgru wcel cpw cv cpr wral crn cuni cmap co w3a wi wtr elgrug ibi
      wa simprd simp1 ralimi wceq pweq eleq1d rcla4cv 3syl imp ) BEFZABFZAGZBFZ
      UJCHZGZBFZUNDHZIBFDBJZUQKLBFDBUNMNJZOZCBJZUPCBJUKUMPUJBQZVAUJVBVATCDBERSU
      AUTUPCBUPURUSUBUCUPUMCABUNAUDUOULBUNAUEUFUGUHUI $.
      $( [9-Jun-2013] $)

    $( Any subset of an element of a Grothendieck's universe is also an
       element.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruss $p |- ( ( U e. Univ /\ A e. U /\ B C_ A ) -> B e. U ) $=
      ( cgru wcel wss wa cpw wb elpw2g adantl grupw grutr syldan sylbird 3impia
      sseld ) CDEZACEZBAFZBCEZRSGZTBAHZEZUASUDTIRBACJKUBUCCBRSUCCEUCCFACLUCCMNQ
      OP $.
      $( [9-Jun-2013] $)

    ${
      $d B x $.
      $( A Grothendieck's universe contains pairs derived from its elements.
         (Contributed by Mario Carneiro, 9-Jun-2013.) $)
      grupr $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> { A , B } e. U ) $=
        ( vx vy cgru wcel cpr cv wral cpw crn cuni cmap w3a wceq eleq1d rcla4cv
        wi co wtr wa elgrug ibi preq2 3ad2ant2 com12 ralimdv syl5com preq1 syl6
        simprd com23 3imp ) CFGZACGZBCGZABHZCGZUOUQUPUSUOUQDIZBHZCGZDCJZUPUSSUO
        UTKCGZUTEIZHZCGZECJZVELMCGECUTNTJZOZDCJZUQVCUOCUAZVKUOVLVKUBDECFUCUDULU
        QVJVBDCVJUQVBVHVDUQVBSVIVGVBEBCVEBPVFVACVEBUTUEQRUFUGUHUIVBUSDACUTAPVAU
        RCUTABUJQRUKUMUN $.
        $( [9-Jun-2013] $)
    $}

    $( A Grothendieck's universe contains the range of any function which takes
       values in the universe (see ~ gruiun for a more intuitive version).
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruurn $p |- ( ( U e. Univ /\ A e. U /\ F : A --> U ) -> U. ran F e. U ) $=
      ( vx vy cgru wcel wf crn cuni wa cmap co elmapg wi cpw wral wceq rcla4cv
      cv cpr w3a wtr elgrug ibi simprd rneq unieqd eleq1d 3ad2ant3 ralimi oveq2
      eleq2d imbi1d 3syl imp sylbird 3impia ) BFGZABGZABCHZCIZJZBGZUSUTKVACBALM
      ZGZVDBACFBNUSUTVFVDOZUSDTZPBGZVHETZUABGEBQZVJIZJZBGZEBVHLMZQZUBZDBQZCVOGZ
      VDOZDBQUTVGOUSBUCZVRUSWAVRKDEBFUDUEUFVQVTDBVPVIVTVKVNVDECVOVJCRZVMVCBWBVL
      VBVJCUGUHUISUJUKVTVGDABVHARZVSVFVDWCVOVECVHABLULUMUNSUOUPUQUR $.
      $( [9-Jun-2013] $)

    $( If ` B ( x ) ` is a family of elements of ` U ` and the index set ` A `
       is an element of ` U ` , then the indexed union ` U_ x e. A B ` is also
       an element of ` U ` , where ` U ` is a Grothendieck's universe.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruiun $p |- ( ( U e. Univ /\ A e. U /\ A. x e. A B e. U )
        -> U_ x e. A B e. U ) $=
      ( vy cgru wcel wral ciun wa cmpt crn cuni wf wfn wss eqid fnmpt cv wrex
      cab rnmpt r19.29 eleq1 biimparc rexlimivw syl ex abssdv syl5eqss sylanbrc
      gruurn 3expia syl5com dfiun2g unieqi syl6eqr eleq1d sylibrd com12 3impia
      wceq df-f ) DFGZBDGZCDGZABHZABCIZDGZVGVDVEJZVIVGVJABCKZLZMZDGZVIVGBDVKNZV
      JVNVGVKBOVLDPVOABCVKDVKQZRVGVLESZCVBZABTZEUAZDAEBCVKVPUBZVGVSEDVGVSVQDGZV
      GVSJVFVRJZABTWBVFVRABUCWCWBABVRWBVFVQCDUDUEUFUGUHUIUJBDVKVCUKVDVEVOVNBDVK
      ULUMUNVGVHVMDVGVHVTMVMAEBCDUOVLVTWAUPUQURUSUTVA $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains unions of its elements.  (Contributed
       by Mario Carneiro, 17-Jun-2013.) $)
    gruuni $p |- ( ( U e. Univ /\ A e. U ) -> U. A e. U ) $=
      ( vx cgru wcel wa cuni cv ciun uniiun wral wss grutr dfss3 gruiun mpd3an3
      sylib syl5eqel ) BDEZABEZFZAGCACHZIZBCAJSTUBBECAKZUCBEUAABLUDABMCABNQCAUB
      BOPR $.
      $( [17-Jun-2013] $)

    $( A Grothendieck's universe contains the range of any function which takes
       values in the universe (see ~ gruiun for a more intuitive version).
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grurn $p |- ( ( U e. Univ /\ A e. U /\ F : A --> U ) -> ran F e. U ) $=
      ( cgru wcel wf w3a crn cpw wss simp1 gruurn grupw syl2anc pwuni a1i gruss
      cuni syl3anc ) BDEZABEZABCFZGZTCHZRZIZBEZUDUFJZUDBETUAUBKZUCTUEBEUGUIABCL
      UEBMNUHUCUDOPUFUDBQS $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains image sets drawn from its members.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruima $p |- ( ( U e. Univ /\ Fun F /\ ( F " A ) C_ U ) ->
        ( A e. U -> ( F " A ) e. U ) ) $=
      ( cgru wcel wfun cima wss w3a wa cdm cin cres wrel wceq simpl2 wf syl3anc
      crn 3syl funrel resres resdm reseq1d syl5eqr rneqd df-ima syl6reqr simpl1
      simpr inss2 a1i gruss wfn wfo funforn fof sylbi fssres sylancl ffn simpl3
      inss1 eqsstr3d df-f sylanbrc grurn eqeltrd ex ) BDEZCFZCAGZBHZIZABEZVLBEV
      NVOJZVLCCKZALZMZSZBVPVKCNZVLVTOVJVKVMVOPZCUAWAVTCAMZSVLWAVSWCWAVSCVQMZAMW
      CCVQAUBWAWDCACUCUDUEUFCAUGUHTZVPVJVRBEZVRBVSQZVTBEVJVKVMVOUIZVPVJVOVRAHZW
      FWHVNVOUJWIVPVQAUKULAVRBUMRVPVSVRUNZVTBHWGVPVKVRCSZVSQZWJWBVKVQWKCQZVRVQH
      WLVKVQWKCUOWMCUPVQWKCUQURVQAVCVQWKVRCUSUTVRWKVSVATVPVTVLBWEVJVKVMVOVBVDVR
      BVSVEVFVRBVSVGRVHVI $.
      $( [9-Jun-2013] $)

    $( Any element of an element of a Grothendieck's universe is also an
       element of the universe.  (Contributed by Mario Carneiro,
       9-Jun-2013.) $)
    gruel $p |- ( ( U e. Univ /\ A e. U /\ B e. A ) -> B e. U ) $=
      ( cgru wcel wa grutr sseld 3impia ) CDEZACEZBAEBCEJKFACBACGHI $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains the singletons of its elements.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grusn $p |- ( ( U e. Univ /\ A e. U ) -> { A } e. U ) $=
      ( cgru wcel wa csn cpr dfsn2 grupr 3anidm23 syl5eqel ) BCDZABDZEAFAAGZBAH
      LMNBDAABIJK $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains ordered pairs of its elements.
       (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    gruop $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> <. A , B >. e. U ) $=
      ( cgru wcel w3a cop csn df-op simp1 grusn 3adant3 grupr syl3anc syl5eqel
      cpr ) CDEZACEZBCEZFZABGAHZABPZPZCABITQUACEZUBCEUCCEQRSJQRUDSACKLABCMUAUBC
      MNO $.
      $( [10-Jun-2013] $)

    ${
      $d B x $.
      $( A Grothendieck's universe contains binary unions of its elements.
         (Contributed by Mario Carneiro, 9-Jun-2013.) $)
      gruun $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> ( A u. B ) e. U ) $=
        ( vx cgru wcel w3a cun cv ciun cuni uniiun wceq uniprg 3adant1 syl5reqr
        cpr wral simp1 eleq1a grupr wa wo vex elpr jaao syl5bi ralrimiv syl3anc
        gruiun eqeltrd ) CEFZACFZBCFZGZABHZDABQZDIZJZCUOUSUQKZUPDUQLUMUNUTUPMUL
        ABCCNOPUOULUQCFURCFZDUQRZUSCFULUMUNSABCUAUMUNVBULUMUNUBZVADUQURUQFURAMZ
        URBMZUCVCVAURABDUDUEUMVDVAUNVEACURTBCURTUFUGUHODUQURCUJUIUK $.
        $( [9-Jun-2013] $)
    $}

    $( A Grothendieck's universe contains binary cartesian products of its
       elements.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruxp $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> ( A X. B ) e. U ) $=
      ( cgru wcel w3a cun cxp gruun cpw grupw wss xpsspw gruss mp3an3 3ad2antl1
      syldan mpdan ) CDEZACEZBCEZFABGZCEZABHZCEZABCISTUCUEUASUCUBJZCEZUEUBCKSUG
      UFJZCEZUEUFCKSUIUDUHLUEABMUHUDCNOQQPR $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains all powers of its elements.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grumap $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> ( A ^m B ) e. U ) $=
      ( cgru wcel w3a cxp cpw cmap wss simp1 gruxp 3com23 grupw syl2anc mapsspw
      co 3ad2ant3 gruss syl3anc ) CDEZACEZBCEZFZUABAGZHZCEZABIQZUFJZUHCEUAUBUCK
      ZUDUAUECEZUGUJUAUCUBUKBACLMUECNOUCUAUIUBABCPRUFUHCST $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains indexed cartesian products of its
       elements.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruixp $p |- ( ( U e. Univ /\ A e. U /\ A. x e. A B e. U )
        -> X_ x e. A B e. U ) $=
      ( cgru wcel wral w3a ciun cmap cixp wss simp1 gruiun simp2 grumap syl3anc
      co ixpssmapg 3adant1 gruss ) DEFZBDFZCDFABGZHZUBABCIZBJRZDFZABCKZUGLZUIDF
      UBUCUDMZUEUBUFDFUCUHUKABCDNUBUCUDOUFBDPQUCUDUJUBABCDDSTUGUIDUAQ $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains indexed intersections of its
       elements.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruiin $p |- ( ( U e. Univ /\ E. x e. A B e. U ) -> |^|_ x e. A B e. U ) $=
      ( vy cgru wcel wrex ciin ax-17 hbii1 cv hbel wss gruss syl3an3 3exp com23
      iinss2 rexlimd imp ) DFGZCDGZABHABCIZDGZUBUCUEABUBAJAEEUDDAEBCKELDGAJMUBU
      CALBGZUEUBUCUFUEUFUBUCUDCNUEABCSCUDDOPQRTUA $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains all functions on its elements.
       (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    gruf $p |- ( ( U e. Univ /\ A e. U /\ F : A --> U ) -> F e. U ) $=
      ( vx vy vz cgru wcel wf w3a cv cfv cop wceq wa copab crn wfn opex eqeltrd
      simp3 ffn fnasrn 3syl wral eqid fnopab2 fveq2 opeq12 mpdan fvopab4 adantl
      a1i simpl1 gruel 3expa 3adantl3 ffvelrn 3ad2antl3 gruop syl3anc ralrimiva
      ffnfv sylanbrc grurn syld3an3 ) BGHZABHZABCIZJZCDKZAHEKVKVKCLZMZNODEPZQZB
      VJVICARCVONVGVHVIUAABCUBDEACUCUDVGVHVIABVNIZVOBHVJVNARZFKZVNLZBHZFAUEVPVQ
      VJDEAVMVNVKVLSVNUFZUGUMVJVTFAVJVRAHZOZVSVRVRCLZMZBWBVSWENVJDEVRVMWEAVNVKV
      RNVLWDNVMWENVKVRCUHVKVLVRWDUIUJWAVRWDSUKULWCVGVRBHZWDBHZWEBHVGVHVIWBUNVGV
      HWBWFVIVGVHWBWFAVRBUOUPUQVIVGWBWGVHABVRCURUSVRWDBUTVATVBFABVNVCVDABVNVEVF
      T $.
      $( [10-Jun-2013] $)

    $( A Grothendieck's universe contains all subsets of itself that are
       equipotent to an element of the universe.  (Contributed by Mario
       Carneiro, 9-Jun-2013.) $)
    gruen $p |- ( ( U e. Univ /\ A C_ U /\ ( B e. U /\ B ~~ A ) )
        -> A e. U ) $=
      ( vy cgru wcel wss cen wbr wa wi cv wf1o wex cvv wb ssexg syl wf com23
      breng wfo f1ofo w3a crn wceq simp3l forn fof sylan grurn syl3an3 eqeltrrd
      fss 3expia exp3a syl5 exlimdv com3r expdimp sylbid imp3a ancoms 3impia )
      CEFZACGZBCFZBAHIZJZACFZVFVEVIVJKVFVEJZVGVHVJVKVHVGVJVKVHBADLZMZDNZVGVJKVK
      AOFVHVNPACEQBAODUARVKVGVNVJVFVEVGVNVJKVEVGJZVNVFVJVOVMVFVJKZDVMBAVLUBZVOV
      PBAVLUCVOVQVFVJVEVGVQVFJZVJVEVGVRUDZVLUEZACVSVQVTAUFVEVGVQVFUGBAVLUHRVRVE
      VGBCVLSZVTCFVQBAVLSVFWABAVLUIBACVLUNUJBCVLUKULUMUOUPUQURUSUTTVATVBVCVD $.
      $( [9-Jun-2013] $)
  $}

  ${
    $d u x y A $.
    $( The intersection of a family of universes is a universe.  (Contributed
       by Mario Carneiro, 9-Jun-2013.) $)
    intgru $p |- ( ( A C_ Univ /\ A =/= (/) ) -> |^| A e. Univ ) $=
      ( vx vy vu cgru wss wa cvv wcel wtr cv wral cmap co elgrug ral2imi elint2
      w3a adantlr wi c0 wne cint cpw cpr crn simpr intex sylib dfss3 ibi simpld
      cuni ralimi sylbi trint syl adantr grupw ex vex pwex 3imtr4g r19.26 grupr
      imp 3expia sylbir sylan2b ralrimiv wf wb elmapg mpan2 ad2antlr intss1 fss
      prex sylan2 ralrimiva gruurn syl5 rnex syl6ibr sylbid 3jca sylanb biimpar
      uniex syl12anc ) AEFZAUAUBZGZAUCZHIZWNJZBKZUDZWNIZWQCKZUEZWNIZCWNLZWTUFZU
      MZWNIZCWNWQMNZLZRZBWNLZWNEIZWMWLWOWKWLUGAUHZUIWKWPWLWKDKZJZDALZWPWKXMEIZD
      ALZXODAEUJZXPXNDAXPXNWRXMIZXAXMIZCXMLXEXMIZCXMWQMNLRBXMLZXPXNYBGBCXMEOUKU
      LUNUODAUPUQURWKXQWLXJXRXQWLGZXIBWNYCWQWNIZGZWSXCXHXQYDWSWLXQYDWSXQWQXMIZD
      ALZXSDALYDWSXPYFXSDAXPYFXSWQXMUSUTPDWQABVAZQZDWRAWQYHVBQVCVFSXQYDXCWLXQYD
      GZXBCWNYDXQYGWTWNIZXBTYIXQYGGZWTXMIZDALZXTDALZYKXBYLXPYFGZDALZYNYOTXPYFDA
      VDZYPYMXTDAXPYFYMXTWQWTXMVEVGPVHDWTACVAZQDXAAWQWTVRQVCVIVJSYEXFCXGYEWTXGI
      ZWQWNWTVKZXFWLYTUUAVLZXQYDWLWOUUBXLWOWQHIUUBYHWNWQWTHHVMVNUOVOXQYDUUAXFTW
      LYJUUAYADALZXFUUAWQXMWTVKZDALZYJUUCUUAUUDDAXMAIUUAWNXMFUUDXMAVPWQWNXMWTVQ
      VSVTYDXQYGUUEUUCTZYIYLYQUUFYRYPUUDYADAXPYFUUDYAWQXMWTWAVGPVHVIWBDXEAXDWTY
      SWCWIQWDSWEVJWFVTWGWOXKWPXJGBCWNHOWHWJ $.
      $( [9-Jun-2013] $)

    $d u U $.
    $( The intersection of a universe with a class that acts like a universe is
       another universe.  (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    ingru $p |- ( ( Tr A /\ A. x e. A ( ~P x e. A /\
          A. y e. A { x , y } e. A /\ A. y ( y : x --> A -> U. ran y e. A ) ) )
        -> ( U e. Univ -> ( U i^i A ) e. Univ ) ) $=
      ( vu cgru wcel wtr cv wral wi w3a wa wss ssralv ax-mp elin ral2imi syl2im
      simplbi2 cpw cpr wf crn cuni wal cin wceq ineq1 eleq1d imbi2d cmap elgrug
      co ibi trin ex inss1 inss2 im2anan9 vex mapss inex1 elmap fss mpan2 sylbi
      mp2b imim1i alimi df-ral sylibr df-3an 3imtr4g syl cvv wb syl6ibr vtoclga
      3impa com12 ) DFGCHZAIZUAZCGZWCBIZUBZCGZBCJZWCCWFUCZWFUDUEZCGZKZBUFZLZACJ
      ZMZDCUGZFGZWQEIZCUGZFGZKWQWSKEDFWTDUHZXBWSWQXCXAWRFWTDCUIUJUKWTFGZWQXAHZW
      DXAGZWGXAGZBXAJZWKXAGZBXAWCULUNZJZLZAXAJZMZXBXDWTHZWDWTGZWGWTGZBWTJZWKWTG
      ZBWTWCULUNZJZLZAWTJZMZWQXNKXDYDABWTFUMUOXOWBXEYCWPXMXOWBXEWTCUPUQYCYBAXAJ
      ZWPWOAXAJZXMXAWTNZYCYEKWTCURZYBAXAWTOPXACNZWPYFKWTCUSZWOAXACOPYBWOXLAXAYB
      WEWIMZWNMZXFXHMZXKMZWOXLXPXRYAYLYNKXPXRMYKYMYAWNXKXPWEXFXRWIXHXFXPWEWDWTC
      QTXRXQBXAJZWIWHBXAJZXHYGXRYOKYHXQBXAWTOPYIWIYPKYJWHBXACOPXQWHXGBXAXGXQWHW
      GWTCQTRSUTYAXSBXJJZWNWLBXJJZXKYGXJXTNYAYQKYHXAWTWCEVAZAVAZVBXSBXJXTOVHWNW
      FXJGZWLKZBUFYRWMUUBBUUAWJWLUUAWCXAWFUCZWJXAWCWFWTCYSVCZYTVDUUCYIWJYJWCXAC
      WFVEVFVGVIVJWLBXJVKVLXSWLXIBXJXIXSWLWKWTCQTRSUTVTWEWIWNVMXFXHXKVMVNRSUTVO
      XAVPGXBXNVQUUDABXAVPUMPVRVSWA $.
      $( [10-Jun-2013] $)

    $( The wellfounded part of a universe is another universe.  (Contributed by
       Mario Carneiro, 17-Jun-2013.) $)
    wfgru $p |- ( U e. Univ -> ( U i^i U. ( R1 " On ) ) e. Univ ) $=
      ( vx vy cr1 con0 cima cuni wtr cv cpw wcel cpr wral wf crn wi cgru r1elss
      wss vex wal w3a cin dftr3 r1elssi mprgbir pwwf wa prss prex bitr4i biimpi
      ralrimiva frn rnex uniwf sylbir syl ax-gen a1i 3jca rgen ingru mp2an ) DE
      FGZHZBIZJVEKZVGCIZLZVEKZCVEMZVGVEVINZVIOZGVEKZPZCUAZUBZBVEMAQKAVEUCQKPVFV
      GVESBVEBVEUDVGUEUFVRBVEVGVEKZVHVLVQVGUGVSVKCVEVSVIVEKUHZVKVTVJVESVKVGVIVE
      BTCTZUIVJVGVIUJRUKULUMVQVSVPCVMVNVESZVOVGVEVIUNWBVNVEKVOVNVIWAUORVNUPUQUR
      USUTVAVBBCVEAVCVD $.
      $( [17-Jun-2013] $)

    $d B x y $.  $d U x y $.
    $( Each ordinal that is comparable with an element of the universe is in
       the universe.  (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    grudomon $p |- ( ( U e. Univ /\ A e. On /\ ( B e. U /\ A ~<_ B ) ) ->
        A e. U ) $=
      ( vx vy wcel cdom wbr wa con0 wi wceq breq1 eleq1 imbi12d imbi2d wral cvv
      cv wss cgru r19.21v w3a simpl1 vex onelss ssdomg mpsyl sylan simplr domtr
      imp syl2anc pm2.27 syl ralimdva dfss3 cen wb domeng 3ad2ant3 biimpa gruss
      wex simpl2 3expia 3adant1 adantr ensym anim12d ancomsd eximdv 3com23 3exp
      a1i gruen exlimdv sylsyld syl5bir syld ex com23 3expib syl5bi tfis3 com3l
      mpd a2d impr 3impia ) CUAFZBCFZABGHZIZAJFZACFZWKWNWOWPWKWLWMWOWPKWOWKWLIZ
      WMWPWQDSZBGHZWRCFZKZKZWQESZBGHZXCCFZKZKZWQWMWPKZKDEAWRXCLZXAXFWQXIWSXDWTX
      EWRXCBGMWRXCCNOPWRALZXAXHWQXJWSWMWTWPWRABGMWRACNOPXGEWRQWQXFEWRQZKWRJFZXB
      WQXFEWRUBXLWQXKXAXLWKWLXKXAKXLWKWLUCZWSXKWTXMWSXKWTKXMWSIZXKXEEWRQZWTXNXF
      XEEWRXNXCWRFZIZXDXFXEKXQXCWRGHZWSXDXNXLXPXRXLWKWLWSUDXCRFXLXPIXCWRTZXREUE
      ZXLXPXSWRXCUFULXCWRRUGUHUIXMWSXPUJXCWRBUKUMXDXEUNUOUPXOWRCTZXNWTEWRCUQXNW
      RXCURHZXCBTZIZEVDZYAWTKZXMWSYEWLXLWSYEUSWKEWRBCUTVAVBXNWKYEXEXCWRURHZIZEV
      DYFXLWKWLWSVEXNYDYHEXNYCYBYHXNYCXEYBYGXMYCXEKZWSWKWLYIXLWKWLYCXEBXCCVCVFV
      GVHYBYGKXNWRXCXTVIVOVJVKVLWKYHYFEWKYHYAWTWKYAYHWTWRXCCVPVMVNVQVRWGVSVTWAW
      BWCWHWDWEWFWIWJVM $.
      $( [10-Jun-2013] $)
  $}

  ${
    $d A x y $.  $d U x y $.
    gruina.1 $e |- A = ( U i^i On ) $.
    $( If a Grothendieck's universe ` U ` is nonempty, then the height of the
       ordinals in ` U ` is a strongly inaccessible cardinal.  (Contributed by
       Mario Carneiro, 17-Jun-2013.) $)
    gruina $p |- ( ( U e. Univ /\ U =/= (/) ) -> A e. Inacc ) $=
      ( vx vy wcel c0 wne cfv wceq csdm wbr wral con0 wss syl adantr cen sylc
      wa cgru ccf cv cpw cina wex wi n0 cin 0ss gruss mp3an3 0elon jctir sylibr
      elin syl6eleqr ne0i expcom exlimiv sylbi impcom wn word cvv wtr cep grutr
      wwe ralrimiva dftr3 tron trin sylancl epweon wess mp2 a1i df-ord sylanbrc
      inss2 inex1g elon2 syl5eqel eloni ordirr biimpri mtod ccrd cuni cint wlim
      cab com wrex inss1 eqsstri sseli cdom vex pwex cardid ensymi grupw syldan
      cardon endom ax-mp grudomon mp3an2 mpanr2 onelss ssdomg endomtr sdomdomtr
      mpsyl sylancr canth2 mpani sylan2 inawinalem winainflem syl3anc wb sdomtr
      iscard cardlim sseq2 limeq bibi12d mpbii mpbid syl2anc eleq1 mpbiri abssi
      cflm fvex syl6eqelr eqeltrd intex onint eqeq1 anbi1d exbidv sylib simp2rr
      w3a simp1l simp2rl syl6ss 3ad2ant3 simp2l syl6eqbr gruen syl112anc gruuni
      elab 3exp exlimdv mpd wo cfon cfle onsseleq mpan ord elina syl3anbrc ) BU
      AFZBGHZTZAGHZAUBIZAJZDUCZUDZAKLZDAMZAUEFUVKUVJUVMUVKUVPBFZDUFUVJUVMUGZDBU
      HUVTUWADUVJUVTUVMUVJUVTTZGAFUVMUWBGBNUIZAUWBGBFZGNFZTGUWCFUWBUWDUWEUVJUVT
      GUVPOUWDUVPUJUVPGBUKULUMUNGBNUPUOCUQAGURPUSUTVAVBZUVLANFZUVNAFZVCUVOUVJUW
      GUVKUVJAUWCNCUVJUWCVDZUWCVEFUWCNFUVJUWCVFZUWCVGVIZUWIUVJBVFZNVFUWJUVJUVPB
      OZDBMUWLUVJUWMDBUVPBVHVJDBVKUOVLBNVMVNUWKUVJUWCNONVGVIUWKBNWAVOUWCNVGVPVQ
      VRUWCVSVTBNUAWBUWCWCVTWDZQZUVLUWHABFZUVLUWGUWPVCUWOUWGUWPAAFZUWGAVDUWQVCA
      WEAWFPUWPUWGUWQUWPUWGTZAUWCAAUWCFUWRABNUPWGCUQUSWHPUVLUVNEUCZWIIZJZUWSAOZ
      AUWSWJZJZTZTZEUFZUWHUWPUGZUVLUVNUVPUWTJZUXETZEUFZDWMZFUXGUVLUVNUXLWKZUXLU
      VLUWGAWLZUVNUXMJUWOUVLWNAOZUXNUVLUVMUWGUVPUWSKLEAWODAMZUXOUWFUWOUVJUXPUVK
      UVJUWGUVSUXPUWNUVJUVRDAUVPAFZUVJUVTUVRABUVPAUWCBCBNWPWQZWRUWBUWGUVQUDZAWS
      LZUVRUVJUWGUVTUWNQUWBUXSUXSWIIZRLUYAAWSLZUXTUYAUXSUVQUVPDWTZXAZXAUXSXBZXC
      UVJUVTUXSBFZUYBUVJUVTUVQBFUYFUVPBXDUVQBXDXEUYANFZUVJUYFTZUYAAOZUYBUXSXFZU
      YHUWGUYAAFZUYIUVJUWGUYFUWNQUYHUYABFZUYGUYKUVJUYFUYAUXSWSLZUYLUYAUXSRLUYMU
      YEUYAUXSXGXHUVJUYGUYFUYMTUYLUYJUYAUXSBXIXJXKUYJUYLUYGTZUYAUWCAUYAUWCFUYNU
      YABNUPWGCUQVNAUYAXLSUYAANXMXPXEUXSUYAAXNXQUWGUVQUXSKLUXTUVRUVQUYDXRUVQUXS
      ANXOXSSXTZVJZDEAYASQDEAYBYCUVJUXOUXNYDZUVKUVJAWIIZAJZUYQUVJUWGUVPAKLZDAMU
      YSUWNUVJUYTDAUVJUXQTUVPUVQKLUVRUYTUVPUYCXRUYOUVPUVQAYEXQVJDAYFVTUYSWNUYRO
      ZUYRWLZYDUYQAYGUYSVUAUXOVUBUXNUYRAWNYHUYRAYIYJYKPQYLDEANYQYMZUVLUXLNOUXLG
      HZUXMUXLFUXKDNUXJUVPNFZEUXIVUEUXEUXIVUEUWTNFUWSXFUVPUWTNYNYOQUTYPUVLUXMVE
      FVUDUVLUXMUVNVEVUCAUBYRZYSUXLUUAUOUXLUUBXQYTUXKUXGDUVNVUFUVPUVNJZUXJUXFEV
      UGUXIUXAUXEUVPUVNUWTUUCUUDUUEUURUUFUVLUXFUXHEUVLUXFUWHUWPUVLUXFUWHUUHZAUX
      CBUXBUXDUXAUVLUWHUUGVUHUVJUWSBFZUXCBFUVJUVKUXFUWHUUIZVUHUVJUWSBOUVNBFZUVN
      UWSRLVUIVUJVUHUWSABUXBUXDUXAUVLUWHUUJUXRUUKUWHUVLVUKUXFABUVNUXRWRUULVUHUV
      NUWTUWSRUVLUXAUXEUWHUUMUWSXBUUNUWSUVNBUUOUUPUWSBUUQYMYTUUSUUTUVAWHUWGUWHU
      VOUVNNFZUWGUWHUVOUVBZAUVCVULUWGTUVNAOVUMAUVDUVNAUVEYKUVFUVGSUVJUVSUVKUYPQ
      DAUVHUVI $.
      $( [17-Jun-2013] $)

    $( A characterization of Grothendieck's universes, part 1.  (Contributed by
       Mario Carneiro, 23-Jun-2013.) $)
    grur1a $p |- ( U e. Univ -> ( R1 ` A ) C_ U ) $=
      ( vx vy wcel cr1 cfv wss c0 wceq wi con0 fveq2 3syl wa sseli eleq1 eleq1d
      wral cgru cin inss1 eqsstri sseq2 mpbii ss0 0ss r10 syl6eq sseq1d a1i wne
      mpbiri cv ciun cina cwina gruina inawina wlim winalim r1lim syl2anc inss2
      winaon csuc imbi12d simpr elelsuc word wb ne0i sylan2 ordsucelsuc syl5ibr
      syl eloni mpd cpw grupw ex adantr r1suc biimprcd syl6 embantd com23 com4r
      ontr1 pm2.27 exp3a com3r sylc imp ralimdva gruiun 3expia syld cvv biimprd
      vex mpan sylan9r exp32 com34 tfinds2 impcom grutr syldan ralrimiva sylibr
      iunss eqsstrd pm2.61dne ) BUAFZAGHZBIZBJBJKZXRLXPXSAJIZAJKZXRXSABIXTABMUB
      ZBCBMUCUDZBJAUEUFAUGYAXRJBIBUHYAXQJBYAXQJGHZJAJGNUIUJUKUNOULXPBJUMZXRXPYE
      PZXQDADUOZGHZUPZBYFAUQFZAURFZXQYIKZABCUSZAUTZYKAMFZAVAYLAVFZAVBDAMVCVDOXP
      YIBIZYEXPYHBIZDATYQXPYRDAXPYGAFZYHBFZYRYSXPYTYSYGMFZXPYTLAMYGAYBMCBMVEUDQ
      UUAXPYSYTYSYTLJAFZJBFZLZEUOZAFZUUEGHZBFZLZUUEVGZAFZUUJGHZBFZLXPDEYGJKZYSU
      UBYTUUCYGJARUUNYHJBUUNYHYDJYGJGNUIUJSVHYGUUEKZYSUUFYTUUHYGUUEARUUOYHUUGBY
      GUUEGNSVHYGUUJKZYSUUKYTUUMYGUUJARUUPYHUULBYGUUJGNSVHUUDXPABJYCQULXPUUIUUK
      UUEMFZUUMXPUUKUUIUUQUUMLZXPUUKUUIUURLXPUUKPZUUFUUHUURUUSUUKUUFXPUUKVIUUKU
      UFUUSUUJAVGFZUUJAVJUUSYOAVKUUFUUTVLUUKXPYEYOUUKUUJBFYEABUUJYCQBUUJVMVQYFY
      JYKYOYMYNYPOZVNAVRUUEAVOOVPVSUUSUUHUUGVTZBFZUURXPUUHUVCLUUKXPUUHUVCUUGBWA
      WBWCUUQUUMUVCUUQUULUVBBUUEWDSWEWFWGWBWHWIYGVAZXPYSUUIEYGTZYTUVDXPYSUVEYTL
      XPYSPZUVEEYGUUGUPZBFZUVDYTUVFUVEUUHEYGTZUVHUVFUUIUUHEYGUVFUUEYGFZUUIUUHLZ
      UVFYSYOUVJUVKLXPYSVIYSXPYEYOYSYGBFZYEABYGYCQZBYGVMVQUVAVNYOUVJYSUVKYOUVJY
      SUVKYOUVJYSPUUFUVKUUEYGAWJUUFUUHWKWFWLWMWNWOWPYSXPUVLUVIUVHLUVMXPUVLUVIUV
      HEYGUUGBWQWRVNWSUVDYTUVHUVDYHUVGBYGWTFUVDYHUVGKDXBEYGWTVCXCSXAXDXEXFXGWMV
      SXHYHBXIXJXKDAYHBXMXLWCXNWBXO $.
      $( [23-Jun-2013] $)

    $( A characterization of Grothendieck's universes, part 2.  (Contributed by
       Mario Carneiro, 24-Jun-2013.) $)
    grur1 $p |- ( ( U e. Univ /\ U e. U. ( R1 " On ) ) -> U = ( R1 ` A ) ) $=
      ( vy vx wcel con0 wa cfv wss wn crnk wceq ad2ant2r syl2anc wi syl cvv wbr
      ccf cgru cr1 cima cuni cv wrex wex nss wo simprr wb simplr simprl r1elssi
      sseld sylc cina cwina wne ne0i gruina sylan2 inawina winaon 3syl rankr1ag
      mtbid w3o rankon word eloni ordtri3or syl2an sylancr 3orass sylib ord mpd
      c0 fveq2 eqeq1d rcla4ev ex ad2antrl ctc tcrank eleq2d wtr grutr ralrimiva
      wral dftr3 sylibr adantr vex tcmin ax-mp wfun wf ffun fvelima mpan ssrexv
      rankf syl2im sylbid jaod exlimdv syl5bi cdom simpll ad2antll csdm simp2bi
      cpw elina eqtrd rankcf fvex domtri mp2an syl6eqbrr grudomon syl112anc cin
      mpbir elin biimpri syl6eleqr ordirr adantl pm2.65i pm2.21i exp32 rexlimdv
      syld pm2.18d grur1a eqssd ) BUAFZBUBGUCUDZFZHZBAUBIZUUCBUUDJZUUCUUEKZDUEZ
      LIZAMZDBUFZUUEUUFEUEZBFZUUKUUDFZKZHZEUGUUCUUJEBUUDUHUUCUUOUUJEUUCUUOUUJUU
      CUUOHZUUKLIZAMZAUUQFZUIZUUJUUPUUQAFZKZUUTUUPUUMUVAUUCUULUUNUJUUPUUKUUAFZA
      GFZUUMUVAUKUUPUUBUULUVCYTUUBUUOULUUCUULUUNUMUUBBUUAUUKBUNUOUPZYTUULUVDUUB
      UUNYTUULHZAUQFZAURFZUVDUULYTBVSUSZUVGBUUKUTABCVAZVBAVCZAVDZVEZNUUKAVFOVGY
      TUULUVBUUTPUUBUUNUVFUVAUUTUVFUVAUURUUSVHZUVAUUTUIUVFUUQGFZUVDUVNUUKVIUVMU
      VOUUQVJAVJZUVNUVDUUQVKAVKZUUQAVLVMVNUVAUURUUSVOVPVQNVRUUPUURUUJUUSUULUURU
      UJPUUCUUNUULUURUUJUUIUURDUUKBUUGUUKMUUHUUQAUUGUUKLVTWAWBWCWDUUPUUSALUUKWE
      IZUCZFZUUJUUPUUQUVSAUUPUVCUUQUVSMUVEUUKWFQWGYTUULUVTUUJPUUBUUNUVFUVRBJZUV
      TUUIDUVRUFZUUJUVFUUKBJZBWHZUWAUUKBWIZYTUWDUULYTUWCEBWKUWDYTUWCEBUWEWJEBWL
      WMWNUUKRFUWCUWDHUWAPEWOUUKBRWPWQOLWRZUVTUWBUUAGLWSUWFXDUUAGLWTWQDAUVRLXAX
      BUUIDUVRBXCXENXFXGVRWCXHXIUUCUUIUUEDBUUCUUGBFZUUIUUEUUCUWGUUIHZHZABFZUVDU
      UEUWIYTUVDUWGAUUGXJSUWJYTUUBUWHXKUWIUVGUVHUVDYTUWGUVGUUBUUIUWGYTUVIUVGBUU
      GUTUVJVBNZUVKUVLVEZUUCUWGUUIUMUWIAUUHTIZUUGXJUWIUWMATIZAUUIUWMUWNMUUCUWGU
      UHATVTXLUWIUVGUWNAMZUWKUVGAVSUSUWOUUKXOAXMSEAWKEAXPXNQXQUWMUUGXJSZUUGUWMX
      MSKZUUGXRUWMRFUUGRFUWPUWQUKUUHTXSDWOUWMUUGRRXTYAYFYBAUUGBYCYDUWLUWJUVDHZU
      UEUWRAAFZUWRABGYEZAAUWTFUWRABGYGYHCYIUVDUWSKZUWJUVDUVPUXAUVQAYJQYKYLYMOYN
      YOYPYQYTUUDBJUUBABCYRWNYS $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d T x y $.
    $( Grothendieck's universes are the same as transitive Tarski's classes,
       part one: a transitive Tarski class is a universe.  (The hard work is in
       ~ tskuni .)  (Contributed by Mario Carneiro, 17-Jun-2013.) $)
    grutsk1 $p |- ( ( T e. Tarski /\ Tr T ) -> T e. Univ ) $=
      ( vx vy ctsk wcel wtr wa cgru cv cpw cpr wral crn cuni cmap w3a ralrimiva
      co adantlr wb simpr tskpw tskpr 3expa wf elmapg tskurn 3expia sylbid 3jca
      ralrimiv elgrug adantr mpbir2and ) ADEZAFZGZAHEZUPBIZJAEZUSCIZKAEZCALZVAM
      NAEZCAUSORZLZPZBALZUOUPUAUQVGBAUQUSAEZGZUTVCVFUOVIUTUPUSAUBSUOVIVCUPUOVIG
      VBCAUOVIVAAEVBUSVAAUCUDQSVJVDCVEVJVAVEEZUSAVAUEZVDUOVIVKVLTUPAUSVADAUFSUQ
      VIVLVDUSAVAUGUHUIUKUJQUOURUPVHGTUPBCADULUMUN $.
      $( [17-Jun-2013] $)
  $}

  ${
    $d x y z $.
    $( Grothendieck's universes are the same as transitive Tarski's classes.
       (The proof in the forward direction requires Foundation.)  (Contributed
       by Mario Carneiro, 24-Jun-2013.) $)
    grutsk $p |- Univ = { x e. Tarski | Tr x } $=
      ( vy vz cgru cv wtr ctsk crab wcel wa c0 wceq 0tsk eleq1 mpbiri con0 cuni
      wi cr1 wral a1i wne cin cfv cima cvv vex unir1 eleqtrri eqid grur1 adantr
      mpan2 cina gruina inatsk syl eqeltrd ex pm2.61dne cpw cpr crn cmap co w3a
      elgrug ibi simpld jca grutsk1 impbii treq elrab bitr4i eqriv ) BDAEZFZAGH
      ZBEZDIZVTGIZVTFZJZVTVSIWAWDWAWBWCWAWBVTKVTKLZWBRWAWEWBKGIMVTKGNOUAWAVTKUB
      ZWBWAWFJZVTVTPUCZSUDZGWAVTWILZWFWAVTSPUEQZIWJVTUFWKBUGUHUIWHVTWHUJZUKUMUL
      WGWHUNIWIGIWHVTWLUOWHUPUQURUSUTWAWCVQVAVTIVQCEZVBVTICVTTWMVCQVTICVTVQVDVE
      TVFAVTTZWAWCWNJACVTDVGVHVIVJVTVKVLVRWCAVTGVQVTVMVNVOVP $.
      $( [24-Jun-2013] $)
  $}

