$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Tarski's classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Tarski $. $( The class of Tarski's classes. $)
  $( A function whose value is the smallest Tarski's classe
     containing a particular set. $)

  $( Extend class definition to include the class of all Tarski's classes. $)
  ctsk $a class Tarski $.

  ${
    $d y z w $.
    $( The class of all Tarski's classes.  Tarski's classes is a phrase coined
       by Grzegorz Bancerek in his article "Tarski's Classes and Ranks" Journal
       of Formalized Mathematics.  Vol 1 no 3 May-August 1990.  A Tarski's
       class is a set whose existence is ensured by Tarski's axiom A (see
       ~ ax-groth and the equivalent axioms).  Axiom A was first presented in
       Tarski's article:  "&Uuml;ber unerreichbare Kardinalzahlen".  Tarski had
       invented the axiom A to enable ZFC to manage inaccessible cardinals.
       Later Grothendieck invented the concept of Grothendieck's universes and
       showed they were equal to transitive Tarski's classes. $)
    df-tsk $a |- Tarski = { y | ( A. z e. y ( ~P z C_ y
      /\ E. w e. y ~P z C_ w ) /\ A. z e. ~P y ( z ~~ y \/ z e. y ) ) } $.
  $}

  ${
    $d A x $.  $d T w x y z $.
    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.) $)
    eltskg $p |- ( T e. V -> ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ E. w e. T ~P z C_ w ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) ) $=
      ( vy cv cpw wss wrex wa wral cen wbr wcel ctsk wceq sseq2 rexeq anbi12d
      wo raleqbi1dv pweq breq2 eleq2 orbi12d raleqbidv df-tsk elab2g ) AFZGZEFZ
      HZUJBFHZBUKIZJZAUKKZUIUKLMZUIUKNZTZAUKGZKZJUJCHZUMBCIZJZACKZUICLMZUICNZTZ
      ACGZKZJECODUKCPZUPVEVAVJUOVDAUKCVKULVBUNVCUKCUJQUMBUKCRSUAVKUSVHAUTVIUKCU
      BVKUQVFURVGUKCUILUCUKCUIUDUEUFSEABUGUH $.
      $( [30-Dec-2010] $)

    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.)
       (Revised by Mario Carneiro, 20-Sep-2014.) $)
    eltsk2g $p |- ( T e. V -> ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ ~P z e. T ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) ) $=
      ( vw wcel ctsk cv cpw wss wrex wa wral cen wo eltskg hbra1 wi wceq r19.26
      wbr pweq sseq1d rcla4cva adantlr vex pwex elpw ssel syl5bir syl rexlimdva
      ralimdaa imdistani 3imtr4i ssid sseq2 rcla4ev anim2i ralimi impbii anbi1i
      mpan2 syl6bb ) BCEBFEAGZHZBIZVEDGZIZDBJZKZABLZVDBMTVDBEZNABHLZKVFVEBEZKZA
      BLZVMKADBCOVKVPVMVKVPVFABLZVIABLZKVQVNABLZKVKVPVQVRVSVQVIVNABVFABPVQVLKZV
      HVNDBVTVGBEZKVGHZBIZVHVNQVQWAWCVLVFWCAVGBVDVGRVEWBBVDVGUAUBUCUDVHVEWBEWCV
      NVEVGVDAUEUFUGWBBVEUHUIUJUKULUMVFVIABSVFVNABSUNVOVJABVNVIVFVNVEVEIZVIVEUO
      VHWDDVEBVGVEVEUPUQVBURUSUTVAVC $.
      $( [20-Sep-2014] $) $( [30-Dec-2010] $)

    $( 1st axiom of a a Tarski's class.  The subsets of an element of a
       Tarski's class belong to the class.  (Contributed by FL, 30-Dec-2010.)
       (The proof was shortened by Mario Carneiro, 20-Sep-2014.) $)
    tskpwss $p |- ( ( T e. Tarski /\ A e. T ) -> ~P A C_ T ) $=
      ( vx vy ctsk wcel cv cpw wss wral wrex wa cen wbr eltskg ibi simpld simpl
      wo ralimi syl wceq pweq sseq1d rcla4cva sylan ) BEFZCGZHZBIZCBJZABFAHZBIZ
      UGUJUIDGIDBKZLZCBJZUKUGUPUHBMNUHBFSCBHJZUGUPUQLCDBEOPQUOUJCBUJUNRTUAUJUMC
      ABUHAUBUIULBUHAUCUDUEUF $.
      $( [30-Dec-2010] $)

    $( 2nd axiom of a a Tarski's class.  The powerset of an element of a
       Tarski's class belongs to the class.  (Contributed by FL, 30-Dec-2010.)
       (The proof was shortened by Mario Carneiro, 20-Sep-2014.) $)
    tskpw $p |- ( ( T e. Tarski /\ A e. T ) -> ~P A e. T ) $=
      ( vx ctsk wcel cv cpw wral wss wa cen wbr eltsk2g ibi simpld simpr ralimi
      wo syl wceq pweq eleq1d rcla4cva sylan ) BDEZCFZGZBEZCBHZABEAGZBEZUEUGBIZ
      UHJZCBHZUIUEUNUFBKLUFBERCBGHZUEUNUOJCBDMNOUMUHCBULUHPQSUHUKCABUFATUGUJBUF
      AUAUBUCUD $.
      $( [20-Sep-2014] $) $( [30-Dec-2010] $)

    $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class is either
       equipotent to the class or an element of the class.  (Contributed by FL,
       30-Dec-2010.)  (Revised by Mario Carneiro, 20-Sep-2014.) $)
    tsken $p |- ( ( T e. Tarski /\ A C_ T ) -> ( A ~~ T \/ A e. T ) ) $=
      ( vx vy ctsk wcel wss cpw cen wbr wo elpw2g biimpar cv wral wa eltskg ibi
      wrex simprd wceq breq1 eleq1 orbi12d rcla4cva sylan syldan ) BEFZABGZABHZ
      FZABIJZABFZKZUHUKUIABELMUHCNZBIJZUOBFZKZCUJOZUKUNUHUOHZBGUTDNGDBSPCBOZUSU
      HVAUSPCDBEQRTURUNCAUJUOAUAUPULUQUMUOABIUBUOABUCUDUEUFUG $.
      $( [20-Sep-2014] $) $( [30-Dec-2010] $)
  $}

  $( The empty set is an inaccessible cardinal.  (Contributed by FL,
     30-Dec-2010.) $)
  0tsk $p |- (/) e. Tarski $=
    ( vx c0 ctsk wcel cv cpw wss wa wral cen wbr wo cvv wb 0ex eltsk2g ral0 csn
    ax-mp wceq elsni enref breq1 mpbiri orcd syl pw0 eleq2s rgen mpbir2an ) BCD
    ZAEZFZBGUMBDHZABIZULBJKZULBDZLZABFZIZBMDUKUOUTHNOABMPSUNAQURAUSURULBRZUSULV
    ADULBTZURULBUAVBUPUQVBUPBBJKBOUBULBBJUCUDUEUFUGUHUIUJ $.
    $( [30-Dec-2010] $)

  $( A element of a Tarski's class is strictly dominated by the class.  JFM
     CLASSES2 th. 1 (Contributed by FL, 22-Feb-2011.) $)
  tsksdom $p |- ( ( T e. Tarski /\ A e. T ) -> A ~< T ) $=
    ( ctsk wcel csdm wbr cpw cdom wa wi canth2g a1i cvv wss pwexg expcom ssdomg
    tskpwss sylsyld com12 jcad sdomdomtr syld imp ) BCDZABDZABEFZUEUFAAGZEFZUHB
    HFZIUGUEUFUIUJUFUIJUEABKLUFUEUJUFUHMDUEUHBNZUJABOUEUFUKABRPUHBMQSTUAAUHBCUB
    UCUD $.
    $( [18-Jun-2013] $) $( [22-Feb-2011] $)

  $( A part of a Tarski's class strictly dominated by the class is an element
     of the class.  JFM CLASSES2 th. 2.  (Contributed by FL, 22-Feb-2011.)
     (The proof was shortened by Mario Carneiro, 20-Sep-2014.) $)
  tskssel $p |- ( ( T e. Tarski /\ A C_ T /\ A ~< T ) -> A e. T ) $=
    ( ctsk wcel wss csdm wbr w3a cen wn sdomnen 3ad2ant3 tsken 3adant3 ord mpd
    wo ) BCDZABEZABFGZHZABIGZJZABDZTRUCSABKLUAUBUDRSUBUDQTABMNOP $.
    $( [20-Sep-2014] $) $( [22-Feb-2011] $)

  $( The subsets of an element of a Tarski's class belong to the class.
     (Contributed by FL, 30-Dec-2010.) $)
  tskss $p |- ( ( T e. Tarski /\ A e. T /\ B C_ A ) -> B e. T ) $=
    ( ctsk wcel wss wa cpw wb elpw2g adantl tskpwss sseld sylbird 3impia ) CDEZ
    ACEZBAFZBCEZPQGZRBAHZEZSQUBRIPBACJKTUACBACLMNO $.
    $( [18-Jun-2013] $)

  $( The intersection of two elements of a Tarski's class belongs to the
     class.  (Contributed by FL, 30-Dec-2010.)  (The proof was shortened by
     Mario Carneiro, 20-Sep-2014.) $)
  tskin $p |- ( ( T e. Tarski /\ A e. T ) -> ( A i^i B ) e. T ) $=
    ( ctsk wcel cin wss inss1 tskss mp3an3 ) CDEACEABFZAGKCEABHAKCIJ $.
    $( [20-Sep-2014] $) $( [30-Dec-2010] $)

  $( A singleton of an element of a Tarski's class belongs to the class.  JFM
     CLASSES2 th. 2 (partly) (Contributed by FL, 22-Feb-2011.) $)
  tsksn $p |- ( ( T e. Tarski /\ A e. T ) -> { A } e. T ) $=
    ( ctsk wcel cpw csn tskpw wss snsspw tskss mp3an3 syldan ) BCDZABDAEZBDZAFZ
    BDZABGMOPNHQAINPBJKL $.
    $( [18-Jun-2013] $) $( [22-Feb-2011] $)

  $( A transitive element of a Tarski's class is a part of the class.  JFM
     CLASSES2 th. 8 (Contributed by FL, 22-Feb-2011.) $)
  tsktrss $p |- ( ( T e. Tarski /\ Tr A /\ A e. T ) -> A C_ T ) $=
    ( ctsk wcel wtr w3a cpw wss simp2 dftr4 sylib tskpwss 3adant2 sstrd ) BCDZA
    EZABDZFZAAGZBRPASHOPQIAJKOQSBHPABLMN $.
    $( [20-Sep-2014] $) $( [22-Feb-2011] $)

  $( If an element of a Tarski's class is an ordinal number, its successor is
     an element of the class.  JFM CLASSES2 th. 6 (partly).  (Contributed by
     FL, 22-Feb-2011.)  (The proof was shortened by Mario Carneiro,
     20-Sep-2014.) $)
  tsksuc $p |- ( ( T e. Tarski /\ A e. On /\ A e. T ) -> suc A e. T ) $=
    ( ctsk wcel con0 w3a cpw csuc wss simp1 tskpw 3adant2 cuni wceq simp2 eloni
    word ordunisuc 3syl eqimss syl sspwuni sylibr tskss syl3anc ) BCDZAEDZABDZF
    ZUFAGZBDZAHZUJIZULBDUFUGUHJUFUHUKUGABKLUIULMZAIZUMUIUNANZUOUIUGAQUPUFUGUHOA
    PARSUNATUAULAUBUCUJULBUDUE $.
    $( [20-Sep-2014] $) $( [22-Feb-2011] $)

  ${
    $d T x $.
    $( A non empty Tarski's class contains the empty set.  (Contributed by FL,
       30-Dec-2010.) $)
    tsk0 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> (/) e. T ) $=
      ( vx c0 wne ctsk wcel cv wex wi wss 0ss tskss mp3an3 expcom exlimiv sylbi
      n0 impcom ) ACDZAEFZCAFZSBGZAFZBHTUAIZBAQUCUDBTUCUATUCCUBJUAUBKUBCALMNOPR
      $.
      $( [18-Jun-2013] $) $( [30-Dec-2010] $)
  $}

  $( One is an element of a non empty Tarski's class.  (Contributed by FL,
     22-Feb-2011.) $)
  tsk1 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 1o e. T ) $=
    ( ctsk wcel c0 wne wa c1o csn df1o2 tsk0 tsksn syldan syl5eqel ) ABCZADEZFG
    DHZAINODACPACAJDAKLM $.
    $( [22-Feb-2011] $)

  $( Two is an element of a non empty Tarski's class.  (Contributed by FL,
     22-Feb-2011.)  (The proof was shortened by Mario Carneiro,
     20-Sep-2014.) $)
  tsk2 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 2o e. T ) $=
    ( ctsk wcel c0 wne c1o c2o tsk1 wa csuc df-2o con0 1on tsksuc mp3an2 syldan
    syl5eqel ) ABCZADEFACZGACAHRSIGFJZAKRFLCSTACMFANOQP $.
    $( [20-Sep-2014] $) $( [22-Feb-2011] $)

  $( If a Tarski's class is not empty it has more than two elements.
     (Contributed by FL, 22-Feb-2011.) $)
  2domtsk $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 2o ~< T ) $=
    ( ctsk wcel c0 wne c2o csdm wbr tsk2 tsksdom syldan ) ABCADEFACFAGHAIFAJK
    $.
    $( [22-Feb-2011] $)

  ${
    $d T x y $.
    $( A nonempty Tarski's class is infinite, because it contains all the
       finite levels of the cumulative hierarchy.  (This proof does not use
       ~ ax-inf .) $)
    tskr1om $p |- ( ( T e. Tarski /\ T =/= (/) ) -> ( R1 " om ) C_ T ) $=
      ( vy vx ctsk wcel c0 wne wa cr1 com cima cv cfv wceq wrex wi fveq2 eleq1d
      con0 syl wfun wfn r1fnon fnfun ax-mp fvelima mpan csuc r10 syl5eqel tskpw
      tsk0 cpw nnon r1suc syl5ibr exp3a adantrd finds2 eleq1 syl5ibcom rexlimiv
      imbi2d com12 ssrdv ) ADEZAFGZHZBIJKZABLZVIEZVHVJAEZVKCLZIMZVJNZCJOZVHVLPZ
      IUAZVKVPISUBVRUCSIUDUECVJJIUFUGVOVQCJVMJEVHVNAEZPVOVQVSFIMZAEVJIMZAEZVJUH
      ZIMZAEZVHCBVMFNVNVTAVMFIQRVMVJNVNWAAVMVJIQRVMWCNVNWDAVMWCIQRVHVTFAUIAULUJ
      VJJEZVFWBWEPVGWFVFWBWEVFWBHWEWFWAUMZAEWAAUKWFWDWGAWFVJSEWDWGNVJUNVJUOTRUP
      UQURUSVOVSVLVHVNVJAUTVCVAVBTVDVE $.
      $( [24-Jun-2013] $)

    $( A nonempty Tarski's class contains the whole finite cumulative
       hierarchy.  (This proof does not use ~ ax-inf .) $)
    tskr1om2 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> U. ( R1 " om ) C_ T ) $=
      ( vy vx ctsk wcel c0 wne wa cr1 com cima cuni cv wrex eluni2 wss wtr con0
      wi syld cfv wceq wfun wfn r1fnon fnfun ax-mp fvelima mpan r1tr treq mpbii
      rexlimivw trss 3syl adantl tskr1om sseld 3exp adantr imp rexlimdva syl5bi
      tskss ssrdv ) ADEZAFGZHZBIJKZLZABMZVJEVKCMZEZCVINVHVKAEZCVKVIOVHVMVNCVIVH
      VLVIEZHVMVKVLPZVNVOVMVPSZVHVOVKIUAZVLUBZBJNZVLQZVQIUCZVOVTIRUDWBUERIUFUGB
      VLJIUHUIVSWABJVSVRQWAVKUJVRVLUKULUMVLVKUNUOUPVHVOVPVNSZVHVOVLAEZWCVHVIAVL
      AUQURVFWDWCSVGVFWDVPVNVLVKAVDUSUTTVATVBVCVE $.
      $( [22-Feb-2011] $)
  $}

  $( A nonempty Tarski's class is infinite.  (Contributed by FL,
     22-Feb-2011.) $)
  tskinf $p |- ( ( T e. Tarski /\ T =/= (/) ) -> om ~<_ T ) $=
    ( ctsk wcel wne com cr1 cima cen wbr cdom omex con0 cvv wf1 wss r111 omsson
    c0 wa f1imaen mp2an ensymi tskr1om wi relen brrelexi ssdomg endomtr sylancr
    mp2b syl ) ABCARDSZEFEGZHIUMAJIZEAJIUMEKLMFNELOUMEHIZPQLMEFKTUAZUBULUMAOZUN
    AUCUOUMMCUQUNUDUPUMEHUEUFUMAMUGUJUKEUMAUHUI $.
    $( [22-Feb-2011] $)

  $( If ` A ` and ` B ` are members of a Tarski's class, their unordered pair
     is also an element of the class.  JFM CLASSES2 th. 3 (partly).
     (Contributed by FL, 22-Feb-2011.)  (The proof was shortened by Mario
     Carneiro, 20-Jun-2013.) $)
  tskpr $p |- ( ( T e. Tarski /\ A e. T /\ B e. T )
    -> { A , B } e. T ) $=
    ( ctsk wcel w3a cpr wss csdm wbr simp1 wa prssg ibi 3adant1 com cdom c0 wne
    ne0i tskinf sylan2 wi cfn prfi isfinite3 sdomdomtr mpani adantr mpd 3adant3
    mpbi tskssel syl3anc ) CDEZACEZBCEZFUOABGZCHZURCIJZURCEUOUPUQKUPUQUSUOUPUQL
    USABCCCMNOUOUPUTUQUOUPLPCQJZUTUPUOCRSVACATCUAUBUOVAUTUCUPUOURPIJZVAUTURUDEV
    BABUEURUFULURPCDUGUHUIUJUKURCUMUN $.
    $( [22-Feb-2011] $)

  $( If ` A ` and ` B ` are members of a Tarski's class, their ordered pair is
     also an element of the class.  JFM CLASSES2 th. 4.  (Contributed by FL,
     22-Feb-2011.) $)
  tskop $p |- ( ( T e. Tarski /\ A e. T /\ B e. T )
    -> <. A , B >. e. T ) $=
    ( ctsk wcel w3a cop csn df-op simp1 tsksn 3adant3 tskpr syl3anc syl5eqel
    cpr ) CDEZACEZBCEZFZABGAHZABPZPZCABITQUACEZUBCEUCCEQRSJQRUDSACKLABCMUAUBCMN
    O $.
    $( [22-Feb-2011] $)

  ${
    $d A x y z $.  $d B x y z $.  $d T x y z $.
    $( A cross product of two parts of a Tarski's class is a part of the
       class.  (Contributed by FL, 22-Feb-2011.)  (The proof was shortened by
       Mario Carneiro, 20-Jun-2013.) $)
    tskxpss $p |- ( ( T e. Tarski /\ A C_ T /\ B C_ T )
    -> ( A X. B ) C_ T ) $=
      ( vz vx vy ctsk wcel wss cxp wa cv cop wceq wrex elxp2 w3a tskop eleq1a
      wi syl 3expib rexlimdvv syl5bi ssrdv xpss12 sstr expcom syl2im 3impib ) C
      GHZACIZBCIZABJZCIZUKCCJZCIZULUMKUNUPIZUOUKDUPCDLZUPHUSELZFLZMZNZFCOECOUKU
      SCHZEFUSCCPUKVCVDEFCCUKUTCHZVACHZVCVDTZUKVEVFQVBCHVGUTVACRVBCUSSUAUBUCUDU
      EACBCUFURUQUOUNUPCUGUHUIUJ $.
      $( [22-Feb-2011] $)
  $}

  ${
    $d T x y $.
    $( A Tarski's class is well-orderable.  (Contributed by Mario Carneiro,
       20-Jun-2013.) $)
    tskwe2 $p |- ( T e. Tarski -> E. x e. On x ~~ T ) $=
      ( vy ctsk wcel cv csdm wbr cpw crab wss con0 wrex wral elpwi tskssel 3exp
      cen wi syl5 ralrimiv rabss sylibr tskwe syl ) BDEZCFZBGHZCBIZJBKZAFBRHALM
      UFUHUGBEZSZCUINUJUFULCUIUGUIEUGBKZUFULUGBOUFUMUHUKUGBPQTUAUHCUIBUBUCCABUD
      UE $.
      $( [20-Jun-2013] $)
  $}

  ${
    $d A t z $.
    $( The intersection of a collection of Tarski's classes is a Tarski's
       class.  (Contributed by FL, 17-Apr-2011.)  (The proof was shortened by
       Mario Carneiro, 20-Sep-2014.) $)
    inttsk $p |- ( ( A C_ Tarski /\ A =/= (/) ) -> |^| A e. Tarski ) $=
      ( vz vt ctsk wss wa wcel cv cpw wral cen wbr wo cvv syl syl2anc ralrimiva
      sylibr wn cdom c0 wne cint simpr intex sylib eltsk2g simpll sselda elinti
      wb imp adantll tskpwss ssint tskpw vex pwex elint2 jca wrex rexnal simplr
      elpwi ssdomg mpsyl ad2antrr intss1 sylc simprr simplll simprl sstrd tsken
      ad2antrl ord mt3d ensym domentr sbth expr rexlimdva syl5bir con1d syl6ibr
      sseldd orrd sylan2 mpbir2and ) ADEZAUAUBZFZAUCZDGZBHZIZWMEZWPWMGZFZBWMJZW
      OWMKLZWOWMGZMZBWMIZJZWLWMNGZWNWTXEFUKWLWKXFWJWKUDAUEUFZBWMNUGOWLWSBWMWLXB
      FZWQWRXHWPCHZEZCAJWQXHXJCAXHXIAGZFZXIDGZWOXIGZXJXHADXIWJWKXBUHUIZXBXKXNWL
      XBXKXNWOAXIUJULUMZWOXIUNPQCWPAUORXHWPXIGZCAJWRXHXQCAXLXMXNXQXOXPWOXIUPPQC
      WPAWOBUQZURUSRUTQWLXCBXDWOXDGWLWOWMEZXCWOWMVDWLXSFZXAXBXTXASXNCAJZXBXTYAX
      AYASXNSZCAVAXTXAXNCAVBXTYBXACAXTXKYBXAXTXKYBFZFZWOWMTLZWMWOTLZXAWONGYDXSY
      EXRWLXSYCVCZWOWMNVEVFYDWMXITLZXIWOKLZYFYDXFWMXIEZYHWLXFXSYCXGVGXKYJXTYBXI
      AVHZVOWMXINVEVIYDWOXIKLZYIYDYLXNXTXKYBVJYDYLXNYDXMWOXIEYLXNMYDADXIWJWKXSY
      CVKXTXKYBVLZWFYDWOWMXIYGYDXKYJYMYKOVMWOXIVNPVPVQWOXICUQVROWMXIWOVSPWOWMVT
      PWAWBWCWDCWOAXRUSWEWGWHQWI $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A w x y z $.
    $( ` ( R1 `` A ) ` for ` A ` a strongly inaccessible cardinal is equipotent
       to ` A ` .  (Contributed by Mario Carneiro, 6-Jun-2013.) $)
    inar1 $p |- ( A e. Inacc -> ( R1 ` A ) ~~ A ) $=
      ( vx vy vz wcel cr1 cfv cdom wbr con0 wceq syl wral wa csdm wi c0 cvv wss
      ccrd vw cina cen cxp ciun wlim cwina inawina winaon winalim r1lim syl2anc
      cv onelon sylan csuc eleq1 fveq2 breq1d imbi12d wne 0sdomg syl5ibr breq1i
      ne0i r10 syl6ibr 3syl wtr word eloni ordtr trsuc ex adantl cpw r1suc fvex
      cardid ensymi pwen ax-mp syl6eqbr winacard eleq2d cardsdom sylancr bitr3d
      wb ccf elina simp3bi pweq rcla4cv sylbird imp ensdomtr syl2an imim12d cun
      expr vex mpan iunex iundom ax-17 hbiu1 hbbr ssdomg mprg mp2 domtr sylancl
      mp2an com sylc domentr eqbrtrd ad2antlr wn wf wrex sylibr cardf wfun ffun
      eleq1d ralimdva impr sseq2d syl5bi ad2antrl sylibd wo iunon onelss iuneq1
      wfn a1i mpd ssiun2 mpsyl endomtr vtoclgaf ssun2 unex xpdom2 ssun1 limomss
      xpdom1 ssexg syl6ss infxpidm eleq1a ordirr nsyli ad2ant2r simpll wex ccom
      cres limord elon r1fnon dffn2 mpbi fco onss fssres simpr simplll syl12anc
      ffn ontr1 biimpd embantd fvres fvco3 mp3an12 eqtrd sylibrd ffnfv sylanbrc
      eleq2 biimpa cardon onelssi reximdva syl5 expdimp ralrimiv resfunexg feq1
      eliun fveq1 rexbidv ralbidv anbi12d cla4ev ee12an cfflb syld simp2bi mt2d
      sseq1d ontri1 eqcom ordequn mtord sylsyld iunss unssd cif id 0elon elimel
      uneq12d elexi onun2i dedth adantr onsseleq mpbid orcomd ord simprbi breq1
      iscard domsdomtr exp43 com4l tfinds2 imp3a mpcom sdomdom breq2 raleqbi1dv
      ralrimiva xpeq12 anidms breq12d vtoclg winainf infxpen onssr1 sbth ) AUBE
      ZAFGZAHIZAVUHHIZVUHAUCIVUGVUHAAUDZHIVUKAUCIZVUIVUGVUHBABUMZFGZUEZVUKHVUGA
      JEZAUFZVUHVUOKVUGAUGEZVUPAUHZAUIZLZVUGVURVUQVUSAUJLBAJUKULVUGVUPVUNAHIZBA
      MZVUOVUKHIZVVAVUGVVBBAVUGVUMAEZNZVUNAOIZVVBVUMJEZVVFVVGVUGVUPVVEVVHVVAAVU
      MUNUOVVHVUGVVEVVGVVEVVGPQAEZQFGZAOIZPZCUMZAEZVVMFGZAOIZPZVVMUPZAEZVVRFGZA
      OIZPZVUGBCVUMQKZVVEVVIVVGVVKVUMQAUQVWCVUNVVJAOVUMQFURUSUTVUMVVMKZVVEVVNVV
      GVVPVUMVVMAUQVWDVUNVVOAOVUMVVMFURUSUTVUMVVRKZVVEVVSVVGVWAVUMVVRAUQVWEVUNV
      VTAOVUMVVRFURUSUTVUGVURVUPVVLVUSVUTVUPVVIQAOIZVVKVVIVWFVUPAQVAZAQVEAJVBVC
      VVJQAOVFVDVGVHVVMJEZVUGVVQVWBPVWHVUGNVVSVVNVVPVWAVUGVVSVVNPZVWHVUGVUPAVIZ
      VWIVVAVUPAVJZVWJAVKZAVLLVWJVVSVVNAVVMVMVNVHVOVWHVUGVVPVWAVWHVVTVVOTGZVPZU
      CIVWNAOIZVWAVUGVVPNVWHVVTVVOVPZVWNUCVVMVQVVOVWMUCIZVWPVWNUCIVWMVVOVVMFVRZ
      VVOVSVTZVVOVWMVVOTVRZWAWBWCVUGVVPVWOVUGVVPVWMAEZVWOVUGVURVXAVVPWIVUSVURVW
      MATGZEZVXAVVPVURVXBAVWMAWDZWEVURVVOREZVUPVXCVVPWIZVWRVUTVVOARJWFZWGWHLVUG
      DUMZVPZAOIZDAMZVXAVWOPVUGVWGAWJGZAKZVXKDAWKZWLVXJVWODVWMAVXHVWMKVXIVWNAOV
      XHVWMWMUSWNLWOWPVVTVWNAWQWRXAWSVNVVEVUMUFZVUGVVQCVUMMZVVGVVEVXOVUGVXPVVGV
      VEVXONZVUGVXPNZNZVUNVUMCVUMVWMUEZWTZHIZVYAAOIZVVGVXOVYBVVEVXRVXOVUNDVUMVX
      HFGZUEZVYAHVUMREZVXOVUNVYEKBXBZDVUMRUKXCVXOVYEVUMVXTUDZHIZVYHVYAHIZVYEVYA
      HIVYDVXTHIZVYIDVUMDVUMVXTVYDVYGCVUMVWMVYGVWTXDZVXHFVRXEVVOVXTHIZVYKCBVXHV
      UMVUMVXHECXFCUAVYDVXTHUAUMZVYDECXFVYNHECXFCUAVUMVWMXGXHVVMVXHKVVOVYDVXTHV
      VMVXHFURUSVVMVUMEZVWQVWMVXTHIZVYMVWSVWMREVYOVWMVXTSVYPVWTCVUMVWMUUAVWMVXT
      RXIUUBVVOVWMVXTUUCWGUUDXJVXOVYHVYAVYAUDZHIZVYQVYAUCIZVYJVYHVUMVYAUDZHIZVY
      TVYQHIZVYRVXTVYAHIZWUAVXTREVXTVYASWUCVYLVXTVUMUUEVXTVYARXIXKVXTVYAVUMVUMV
      XTVYGVYLUUFZVYGUUGWBVUMVYAHIZWUBVYFVUMVYASWUEVYGVUMVXTUUHZVUMVYARXIXKVUMV
      YAVYAWUDWUDUUJWBVYHVYTVYQXLXNVXOXOVYAHIZVYSVXOXOREZXOVYASWUGVXOXOVUMSVYFW
      UHVUMUUIZVYGXOVUMRUUKXMVXOXOVUMVYAWUIWUFUULXOVYARXIXPVYAWUDUUMLVYHVYQVYAX
      QWGVYEVYHVYAXLWGXRXSVXSVYAAEZVYCVXSVYAAKZXTWUJVXSWUKAVUMKZAVXTKZVVEVUGWUL
      XTZVXOVXPVVEVUGWUNVVEWULAAEZVUGVUMAAUUNVUGVUPVWKWUOXTVVAVWLAUUOVHUUPWPUUQ
      VXSWUMVVEVVEVXOVXRUURZVXSWUMAVUMSZVVEXTZVXSWUMVXLVUMSZWUQVXSWUMVUMAVYNYAZ
      VXHVVMVYNGZSZCVUMYBZDAMZNZUAUUSZWUSVXSVUMATFUUTZVUMUVAZYAZWUMVXHVVMWVHGZS
      ZCVUMYBZDAMZWVFVXSWVHVUMYRZWVJAEZCVUMMZWVIVXSVVHVUMJWVHYAZWVNVXOVVHVVEVXR
      VXOVUMVJZVVHVUMUVBVUMVYGUVCYCZXSZVVHJJWVGYAZVUMJSWVQRJTYAZJRFYAZWWAYDFJYR
      WWCUVDJFUVEUVFZJRJTFUVGXNZVUMUVHJJVUMWVGUVIWGVUMJWVHUVMVHVXQVUGVXPWVPVXQV
      UGNZVVQWVOCVUMWWFVYONZVVQVXAWVOWWGVVNVVPVXAWWGVUPVYOVVEVVNVUGVUPVXQVYOVVA
      XSZWWFVYOUVJVVEVXOVUGVYOUVKVUPVYOVVENVVNVVMVUMAUVNWPUVLWWGVVPVXAWWGVXCVVP
      VXAWWGVXEVUPVXFVWRWWHVXGWGWWGVXBAVWMVUGVXBAKZVXQVYOVUGVURWWIVUSVXDLZXSWEW
      HUVOUVPZWWGWVJVWMAWWFVVHVYOWVJVWMKVXOVVHVVEVUGWVSXSVVHVYONZWVJVVMWVGGZVWM
      VYOWVJWWMKVVHVVMVUMWVGUVQVOWWLVWHWWMVWMKZVUMVVMUNTYEZWWCVWHWWNWWBWWOYDRJT
      YFWBWWDJRVVMTFUVRUVSLUVTZUOYGUWAYHYICVUMAWVHUWBUWCVXSVVHWUMWVMPWVTVVHWUMW
      VMVVHWUMNWVLDAVVHWUMVXHAEZWVLWUMWWQNVXHVXTEZVVHWVLWUMWWQWWRAVXTVXHUWDUWEW
      WRVXHVWMEZCVUMYBVVHWVLCVXHVUMVWMUWNVVHWWSWVKCVUMWWSWVKWWLVXHVWMSVWMVXHVVO
      UWFZUWGWWLWVJVWMVXHWWPYJVCUWHYKUWIUWJUWKVNLWVEWVIWVMNUAWVHWVGYEZVYFWVHREW
      WAWXAWWEJJWVGYFWBVYGWVGVUMRUWLXNVYNWVHKZWUTWVIWVDWVMVUMAVYNWVHUWMWXBWVCWV
      LDAWXBWVBWVKCVUMWXBWVAWVJVXHVVMVYNWVHUWOYJUWPUWQUWRUWSUWTVXSVUPVVHWVFWUSP
      VUGVUPVXQVXPVVAYLZWVTDCAVUMUAUXAULUXBVUGWUSWUQWIVXQVXPVUGVXLAVUMVUGVWGVXM
      VXKVXNUXCUXEYLYMVXSVUPVVHWUQWURWIWXCWVTAVUMUXFULYMUXDVXSVVHVXTJEZWUKWULWU
      MYNZPWVTVWMJEZWXDCVUMCVUMVWMVYGVWTYOWXFVYOWWTYSXJWUKAVYAKZVVHWXDNWXEVYAAU
      XGVVHWVRVXTVJWXGWXEPWXDVUMVKVXTVKAVUMVXTUXHWRYKXMUXIVXSWUKWUJVXSWUJWUKVXS
      VYAASZWUJWUKYNZVXSVUMVXTAVXSVUPVVEVUMASWXCWUPAVUMYPXPVXSVWMASZCVUMMZVXTAS
      VXQVUGVXPWXKWWFVVQWXJCVUMWWGVUPVVQVXAWXJWWHWWKAVWMYPUXJYHYICVUMVWMAUXKYCU
      XLVXQVYAJEZVUPWXHWXIWIVXRVXOWXLVVEVXOVVHWXLWVSVVHWXLVVHVUMQUXMZCWXMVWMUEZ
      WTZJEVUMQVUMWXMKZVYAWXOJWXPVUMWXMVXTWXNWXPUXNCVUMWXMVWMYQUXQYGWXMWXNVUMQJ
      UXOUXPZWXFWXNJECWXMCWXMVWMWXMJWXQUXRVWTYOWXFVVMWXMEWWTYSXJUXSUXTLVOVUGVUP
      VXPVVAUYAVYAAUYBWRUYCUYDUYEYTVXSWWIVXHAOIZDAMZWUJVYCPVUGWWIVXQVXPWWJYLWWI
      VUPWXSDAUYHUYFWXRVYCDVYAAVXHVYAAOUYGWNVHYTVUNVYAAUYIULUYJUYKUYLUYMUYNVUNA
      UYOLUYRVUNVXHHIZBVXHMZBVXHVUNUEZVXHVXHUDZHIZPVVCVVDPDAJVXHAKZWYAVVCWYDVVD
      WXTVVBBVXHAVXHAVUNHUYPUYQWYEWYBVUOWYCVUKHBVXHAVUNYQWYEWYCVUKKVXHAVXHAUYSU
      YTVUAUTBVXHVXHVUNDXBZWYFVUMFVRXEVUBXPXRVUGVUPXOASZVULVVAVUGVURWYGVUSAVUCL
      AVUDULVUHVUKAXQULVUGVUPAVUHSZVUJVVAVUGVURVUPWYHVUSVUTAVUEVHAVUHJXIXPVUHAV
      UFUL $.
      $( [6-Jun-2013] $)
  $}

  ${
    $( The set of hereditarily finite sets is countable.  TODO:  Prove without
       AC and Infinity. $)
    r1om $p |- ( R1 ` om ) ~~ om $=
      ( com cina wcel cr1 cfv cen wbr omina inar1 ax-mp ) ABCADEAFGHAIJ $.
      $( [27-May-2013] $)
  $}

  ${
    $d A w x y z $.
    $( Any set must be at least as large as the cofinality of its rank, because
       the ranks of the elements of ` A ` form a cofinal map into
       ` ( rank `` A ) ` . $)
    rankcf $p |- -. A ~< ( cf ` ( rank ` A ) ) $=
      ( vx vy vw vz crnk cfv c0 wceq con0 wrex cvv wcel wa ccf wbr fveq2 syl wi
      cdom cv csuc wlim w3o csdm wn rankon onzsl mpbi cf0 0dom syl6eqbr domnsym
      syl6eq c1o cfsuc sylan9eqr wne nsuceq0 neeq1 mpbiri rankonid necon3i cima
      0elon cr1 cuni rankvaln necon1ai breq2 vex 0sdom1dom 0sdom bitr3i vtoclbg
      wb mpbird adantl eqbrtrd rexlimiva nlim0 limeq mtbiri con4i fveq2d breq12
      csn cab mpdan rexeq abbidv mpancom imbi12d cmpt crn eqid cen cfon sdomdom
      rnmpt rcla4ev sylancr ondomen wfo wfn fnmpt fvex mprg dffn4 fodomnum 3syl
      a1i mpi syl5eqbrr vtoclg wss r1elssi sselda ranksnb rankelb limsuc sylibd
      eqeltrd eleq1a rexlimdva abssdv ciun snex dfiun2 iunid eqtr3i fveq2i snwf
      imp abrexexg eleq1 sseq1 r1elss rankuni2b syl5eqr abrexco unieqi mpd3an23
      eqtri syl6req cfslb domtr ex syld pm2.01d 3jaoi ax-mp ) AFGZHIZUUMBUAZUBZ
      IZBJKZUUMLMZUUMUCZNZUDZAUUMOGZUEPZUFZUUMJMUVBAUGBUUMUHUIUUNUVEUURUVAUUNUV
      CATPZUVEUUNUVCHATUUNUVCHOGHUUMHOQUJUNAUKULUVCAUMZRUURUVFUVEUUQUVFBJUUOJMZ
      UUQNUVCUOATUUQUVHUVCUUPOGUOUUMUUPOQUUOUPUQUUQUOATPZUVHUUQUUMHURZUVIUUQUVJ
      UUPHURUUOUSUUMUUPHUTVAUVJUVIAHURZAHUUMHAHIUUMHFGZHAHFQHJMUVLHIVEHVBUIUNVC
      UVJAVFJVDVGZMZUVIUVKVPUVNUUMHAVHZVIUOCUAZTPZUVPHURZUVIUVKCAUVMUVPAUOTVJUV
      PAHUTUVQHUVPUEPUVRUVPCVKZVLUVPUVSVMVNVORVQRVRVSVTUVGRUUTUVEUUSUUTUVDUUTUV
      DDUAZUUOWGZFGZIZBAKZDWHZATPZUVEUUTUVNUVDUWFSZUVNUUTUVNUFUUNUUTUFUVOUUNUUT
      HUCWAUUMHWBWCRWDZUVPUVPFGZOGZUEPZUWCBUVPKZDWHZUVPTPZSUWGCAUVMUVPAIZUWKUVD
      UWNUWFUWOUWJUVCIUWKUVDVPUWOUWIUUMOUVPAFQWEUVPAUWJUVCUEWFWIUWMUWEIUWOUWNUW
      FVPUWOUWLUWDDUWCBUVPAWJWKUWMUWEUVPATWFWLWMUWKUWMBUVPUWBWNZWOZUVPTBDUVPUWB
      UWPUWPWPZWTUWKUVPUUOTPZBJKZUUOUVPWQPBJKZUWQUVPTPZUWKUWJJMUVPUWJTPZUWTUWIW
      RUVPUWJWSUWSUXCBUWJJUUOUWJUVPTVJXAXBBBUVPXCUXAUVPUWQUWPXDZUXBUWPUVPXEZUXD
      UWBLMZUXEBUVPBUVPUWBUWPLUWRXFUXFUUOUVPMUWAFXGXLXHUVPUWPXIUIBUVPUWQUWPUVSX
      JXMXKXNXORUUTUVCUWETPZUWFUVESUUTUWEUUMXPUWEVGZUUMIZUXGUUTUWDDUUMUUTUWCUVT
      UUMMZBAUUTUUOAMZNZUWBUUMMUWCUXJSUXLUWBUUOFGZUBZUUMUXLUUOUVMMZUWBUXNIUUTAU
      VMUUOUUTUVNAUVMXPUWHAXQZRXRUUOXSRUUTUXKUXNUUMMZUUTUXKUXMUUMMZUXQUUTUVNUXK
      UXRSUWHUUOAXTRUUMUXMYAYBYNYCUWBUUMUVTYDRYEYFUUTUVNUXIUWHUVNUUMEUVPUWAIZBA
      KZCWHZEUAZFGZYGZUXHUVNUUMUYAVGZFGZUYDUYEAFBAUWAYGUYEABCAUWAUUOYHZYIBAYJYK
      YLUVNUYAUVMMZUYFUYDIUVNUYHUYAUVMXPZUVNUXTCUVMUVNUXSUVPUVMMZBAUVNUXKNUXOUW
      AUVMMUXSUYJSUVNAUVMUUOUXPXRUUOYMUWAUVMUVPYDXKYEYFUVNUYALMUYHUYIVPBCAUWAUV
      MYOUYBUVMMUYBUVMXPUYHUYIEUYALUYBUYAUVMYPUYBUYAUVMYQUYBEVKYRVORVQEUYAYSRYT
      UYDUVTUYCIEUYAKDWHZVGUXHEDUYAUYCUYBFXGYIUYKUWEDECBAUWAUYCUWBUYGUYBUWAFQUU
      AUUBUUDUUERUUMUWEAFXGUUFUUCUXGUWFUVEUXGUWFNUVFUVEUVCUWEAUUGUVGRUUHRUUIUUJ
      VRUUKUUL $.
      $( [27-May-2013] $)
  $}

  ${
    $d A w x y z $.
    $( ` ( R1 `` A ) ` for ` A ` a strongly inaccessible cardinal is a Tarski's
       class.  (Contributed by Mario Carneiro, 8-Jun-2013.) $)
    inatsk $p |- ( A e. Inacc -> ( R1 ` A ) e. Tarski ) $=
      ( vx vy wcel cr1 cfv wss wa wral wbr wo con0 wceq syl sylbid imp csdm ccf
      wi wb cina cpw cen ctsk cwina inawina wrex ciun wlim winaon winalim r1lim
      cv syl2anc eleq2d eliun syl6bb csuc onelon sylan r1pw limsuc r1ord2 sseld
      rexlimdva cuni elssuni r1tr2 syl6ss ralrimiva crnk r1suc rankr1ai syl6bir
      jca fvex elsuc sylib orcomd cdom wn elpwi ssdomg ad2antlr rankcf fveq2 c0
      mpd elina simp2bi sylan9eqr breq2d mtbii inar1 sdomentr exp3acom23 adantr
      wne sylc mtod adantlr bren2 sylanbrc cima r1elwf rankr1ag biimprd orim12d
      ex cvv eltsk2g ax-mp ) AUADZBUMZUBZAEFZGZXOXPDZHZBXPIZXNXPUCJZXNXPDZKZBXP
      UBZIZXPUDDZXMXSBXPXMYBHZXQXRYGXRXQXMYBXRXMAUEDZYBXRSAUFZYHYBXNCUMZEFZDZCA
      UGZXRYHYBXNCAYKUHZDYMYHXPYNXNYHALDZAUIZXPYNMAUJZAUKZCALULUNUOCXNAYKUPUQYH
      YLXRCAYHYJADZHZYLXOYJURZEFZDZXRYTYJLDZYLUUCTYHYOYSUUDYQAYJUSUTXNYJVANYTUU
      BXPXOYHYSUUBXPGZYHYSUUAADZUUEYHYPYSUUFTYRAYJVBNYHYOUUFUUESYQUUAAVCNOPVDOV
      EONPZXRXOXPVFXPXOXPVGAVHVINUUGVOVJXMYCBYDXMXNYDDZHZXNVKFZAMZUUJADZKYCUUIU
      ULUUKUUIUUJAURZDZUULUUKKXMUUHUUNXMUUHXNUUMEFZDZUUNXMYOUUPUUHTXMYHYOYIYQNZ
      YOUUOYDXNAVLUONZXNUUMVMVNPUUJAXNVKVPVQVRVSUUIUUKYAUULYBUUIUUKYAUUIUUKHXNX
      PVTJZXNXPQJZWAZYAUUHUUSXMUUKUUHXNXPGUUSXNXPWBXNXPYDWCWHWDXMUUKUVAUUHXMUUK
      HZUUTXNAQJZUVBXNUUJRFZQJUVCXNWEUVBUVDAXNQUUKXMUVDARFZAUUJARWFXMAWGWRUVEAM
      XOAQJBAIBAWIWJWKWLWMXMUUTUVCSZUUKXMYOXPAUCJZUVFUUQAWNYOUUTUVGUVCXNXPALWOW
      PWSWQWTXAXNXPXBXCXIUUIYBUULUUIXNELXDVFDZYOYBUULTXMUUHUVHXMUUHUUPUVHUURXNU
      UMXEVNPXMYOUUHUUQWQXNAXFUNXGXHWHVJXPXJDYFXTYEHTAEVPBXPXJXKXLXC $.
      $( [8-Jun-2013] $)
  $}

  $( The set of hereditarily finite sets is a Tarski's class.  (The
     Tarski-Grothendieck Axiom is not needed for this theorem.) $)
  r1omtsk $p |- ( R1 ` om ) e. Tarski $=
    ( com cina wcel cr1 cfv ctsk omina inatsk ax-mp ) ABCADEFCGAHI $.
    $( [28-May-2013] $)

  ${
    $d T x y $.  $d A x $.
    $( A Tarski's class contains all ordinals smaller than it. $)
    tskord $p |- ( ( T e. Tarski /\ A e. On /\ A ~< T ) -> A e. T ) $=
      ( vx vy con0 wcel csdm wbr cv wa wceq breq1 anbi2d eleq1 imbi12d wral cvv
      wi wss imp ctsk simplrl vex onelss ssdomg mpsyl adantlr simplrr domsdomtr
      cdom syl2anc pm2.27 ralimdva dfss3 tskssel 3exp syl5bir com23 adantl syld
      ex tfis3 3impib 3com12 ) AEFZBUAFZABGHZABFZVEVFVGVHVFCIZBGHZJZVIBFZRVFDIZ
      BGHZJZVMBFZRZVFVGJZVHRCDAVIVMKZVKVOVLVPVSVJVNVFVIVMBGLMVIVMBNOVIAKZVKVRVL
      VHVTVJVGVFVIABGLMVIABNOVIEFZVKVQDVIPZVLWAVKWBVLRWAVKJZWBVPDVIPZVLWCVQVPDV
      IWCVMVIFZJZVFVNVQVPRWAVFVJWEUBWFVMVIUJHZVJVNWAWEWGVKVMQFWAWEJVMVISZWGDUCW
      AWEWHVIVMUDTVMVIQUEUFUGWAVFVJWEUHVMVIBUIUKVOVPULUKUMVKWDVLRZWAVFVJWIVFWDV
      JVLWDVIBSZVFVJVLRDVIBUNVFWJVJVLVIBUOUPUQURTUSUTVAURVBVCVD $.
      $( [8-Jun-2013] $)
  $}

  ${
    $d T w x y z $.
    $( An even more direct relationship than ~ r1tskina to get an inacessible
       cardinal out of a Tarski's class: the size of any nonempty Tarski's
       class is an inaccessible cardinal.  (Contributed by Mario Carneiro,
       9-Jun-2013.) $)
    tskcard $p |- ( ( T e. Tarski /\ T =/= (/) ) -> ( card ` T ) e. Inacc ) $=
      ( vx vz vy wcel c0 cfv ccf csdm wbr cmap wss con0 cdom cen cvv syl adantr
      wa syl2anc ctsk wne ccrd wceq cv cpw wral cardeq0 necon3bid biimpar wn co
      cina cale crab cint cmpt eqid pwcfsdom com pwex canth2 simpl cardon oneli
      adantl cardsdomelir tskord syl3anc tskpw tskpwss syldan ssdomg mpsyl wrex
      vex tskwe2 cardid2 ensymg domentr wi sdomdomtr ax-mp ralrimiva inawinalem
      sylancr winainflem mp3an2 sylan2 cardidm cardaleph sylancl fveq2d oveq12d
      mpd breq12d mpbiri ovex w3a simp1 simp3 cxp wf elmap fssxp sylbi ex ssrdv
      fvex cfle sstr mpan tskxpss 3exp com23 mpdi sstr2 syl2im sylc wfn fndmeng
      simp2 ensym ensdomtr syl2an tskssel 3expia domnsym mt2d cfon onsseli mpbi
      ffn wo ori elina syl3anbrc ) AUAEZAFUBZSZAUCGZFUBZUUAHGZUUAUDZBUEZUFZUUAI
      JZBUUAUGZUUAUMEYRUUBYSYRUUAFAFAUAUHUIUJZYTUUCUUAEZUKUUDYTUUJUUAUUAUUCKULZ
      IJZYTUULUUAUUEUNGLBMUOUPZUNGZUUNUUNHGZKULZIJCUUMDCUUOUUECUEDUEZGNJBMUOUQZ
      BUURURUSYTUUAUUNUUKUUPIYTUTUUALZUUAUCGUUAUDUUAUUNUDYTUUBUUHUUSUUIYRUUHYSY
      RUUGBUUAYRUUEUUAEZSZUUFUUFUFZIJZUVBUUANJZUUGUUFUUEBVPZVAZVBUVAUVBANJZAUUA
      OJZUVDUVBPEUVAUVBALZUVGUUFUVFVAYRUUTUUEAEZUVIUVAYRUUEMEZUUEAIJZUVJYRUUTVC
      UUTUVKYRUUAUUEAVDZVEVFUUTUVLYRUUEAVGVFUUEAVHVIZYRUVJUUFAEUVIUUEAVJUUFAVKV
      LVLUVBAPVMVNYRUVHUUTYRUUAAOJZUVHYRUUQAOJDMVOUVODAVQDAVRQUUAAUAVSWOZRUVBAU
      UAVTTUUAMEZUVCUVDSUUGWAUVMUUFUVBUUAMWBWCWFWDRZUUHUUBUUEUUQIJDUUAVOBUUAUGZ
      UUSUVQUUHUVSWAUVMBDUUAWEWCUUBUVQUVSUUSUVMBDUUAWGWHWITAWJBUUAWKWLZYTUUAUUN
      UUCUUOKUVTYTUUAUUNHUVTWMWNWPWQYRUUJUULUKZWAYSYRUUJUWAYRUUJSZUUKUUANJZUWAU
      WBUUKANJZUVHUWCUUKPEUWBUUKALUWDUUAUUCKWRUWBBUUKAYRUUJUUEUUKEZUVJYRUUJUWEW
      SZYRUUEALZUVLUVJYRUUJUWEWTZUWFUWEYRUWGYRUUJUWEXAZUWHUWEUUEUUCUUAXBZLZYRUW
      JALZUWGUWEUUCUUAUUEXCZUWKUUAUUCUUEAUCXIUUAHXIZXDZUUCUUAUUEXEXFYRUUAALZUWL
      YRBUUAAYRUUTUVJUVNXGXHYRUWPUUCALZUWLUUCUUALZUWPUWQUUAXJZUUCUUAAXKXLYRUWQU
      WPUWLYRUWQUWPUWLUUCUUAAXMXNXOXPWOUUEUWJAXQXRXSUWFUWEUUJUVLUWIYRUUJUWEYBUW
      EUUEUUCOJZUUCAIJUVLUUJUWEUUCUUEOJZUWTUWEUWMUXAUWOUWMUUEUUCXTUUCPEUXAUUCUU
      AUUEYMUWNUUCPUUEYAWLXFUUCUUEUVEYCQUUCAVGUUEUUCAYDYETUUEAYFVIYGXHUUKAPVMVN
      YRUVHUUJUVPRUUKAUUAVTTUUKUUAYHQXGRYIUUJUUDUWRUUJUUDYNUWSUUCUUAUUAYJUVMYKY
      LYOQUVRBUUAYPYQ $.
      $( [9-Jun-2013] $)
  $}

  ${
    $d A x $.
    $( There is a direct relationship between transitive Tarski's classes and
       inacessible cardinals: the Tarski's classes that occur in the cumulative
       hierarchy are exactly at the strongly inaccessible cardinals.
       (Contributed by Mario Carneiro, 8-Jun-2013.) $)
    r1tskina $p |- ( A e. On ->
        ( ( R1 ` A ) e. Tarski <-> ( A = (/) \/ A e. Inacc ) ) ) $=
      ( vx con0 wcel cr1 cfv ctsk c0 wceq cina wo wa wn wne cvv cen wbr syl2anc
      ccrd csdm df-ne fvex simplr simpll crnk csuc cima cuni onwf sseli rankr1c
      eqid mpbii simpld rankonid biimpi fveq2d eleq2d mtbid adantl onssr1 tsken
      syl wss sylan2 ord mt3d carden2b mpsyl cv wral simpl adantr tsksdom ensym
      sselda sdomentr imp syl12anc ralrimiva iscard sylanbrc eqtr3d r10 on0eln0
      biimpar r1sdom syldan syl5eqbrr 0sdom adantlr tskcard eqeltrrd ex syl5bir
      sylib orrd fveq2 syl6eq 0tsk syl6eqel inatsk jaoi impbid1 ) ACDZAEFZGDZAH
      IZAJDZKZXEXGXJXEXGLZXHXIXHMAHNZXKXIAHUAXKXLXIXKXLLZXFSFZAJXMASFZXNAXFODXM
      AXFPQZXOXNIAEUBZXMXGXEXPXEXGXLUCZXEXGXLUDXGXELZXPAXFDZXEXTMXGXEAAUEFZEFZD
      ZXTXEYCMZAYAUFEFDZXEAECUGUHZDZYDYELZCYFAUIUJYGYAYAIYHYAULAYAUKUMVCUNXEYBX
      FAXEYAAEXEYAAIAUOUPUQURUSUTXSXPXTXEXGAXFVDZXPXTKAVAZAXFVBVEVFVGZRAXFOVHVI
      XKXOAIZXLXKXEBVJZATQZBAVKYLXEXGVLXKYNBAXKYMADZLZXEYMXFTQZXFAPQZYNXEXGYOUD
      ZYPXGYMXFDYQXEXGYOUCZXKAXFYMXEYIXGYJVMVPYMXFVNRYPXGXEYRYTYSXSXPYRYKAXFXQV
      OVCRXEYQYRLYNYMXFACVQVRVSVTBAWAWBVMWCXMXGXFHNZXNJDXRXEXLUUAXGXEXLLZHXFTQU
      UAUUBHHEFZXFTWDXEXLHADZUUCXFTQXEUUDXLAWEWFAHWGWHWIXFXQWJWPWKXFWLRWMWNWOWQ
      WNXHXGXIXHXFHGXHXFUUCHAHEWRWDWSWTXAAXBXCXD $.
      $( [8-Jun-2013] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d F y $.
    $( A version of ~ fodom that doesn't require the Axiom of Choice ~ ax-ac .
       This version uses a sethood predicate as an antecedent.  (Contributed by
       Mario Carneiro, 23-Jun-2013.) $)
    fodomnumg $p |- ( ( A e. V /\ E. x e. On x ~~ A
        /\ F : A -onto-> B ) -> B ~<_ A ) $=
      ( vy wcel cv cen wbr con0 wrex wfo cdom wa wi wceq breq2 rexbidv foeq2
      anbi12d imbi12d vex fodomnum imp vtoclg 3impib ) BEGAHZBIJZAKLZBCDMZCBNJZ
      UHFHZIJZAKLZUMCDMZOZCUMNJZPUJUKOZULPFBEUMBQZUQUSURULUTUOUJUPUKUTUNUIAKUMB
      UHIRSUMBCDTUAUMBCNRUBUOUPURAUMCDFUCUDUEUFUG $.
      $( [22-Jun-2013] $)
  $}

  ${
    $d A f x y z $.  $d T f x y z $.
    $( The union of an element of a transitive Tarski's class is in the set.
       (Contributed by Mario Carneiro, 22-Jun-2013.) $)
    tskuni $p |- ( ( T e. Tarski /\ Tr T /\ A e. T ) -> U. A e. T ) $=
      ( vf vz vx vy wcel cen wbr wa wceq wrex csdm con0 syl wi syl2anc cdom cvv
      wss ctsk wtr w3a cuni wn ccrd cfv cv wf1o wex cima cab wne tsksdom tskwe2
      ccf cardid2 ensymg mpd adantr cardon sdomentr ax-mp sdomdom breq2 rcla4ev
      sylancr ondomen cmpt crn eqid rnmpt wfo wfn fnmpt vex a1i mprg dffn4 mpbi
      imaexg fodomnumg mp3an3 syl5eqbrr sylan2 domsdomtr sylancom adantll mpdan
      cina c0 ne0i tskcard cpw wral elina simp2bi breqtrrd 3adant2 wlim inawina
      cwina winalim 3syl eqeq1 rexbidv elab imassrn f1ofo syl5sseq ad2antlr wf1
      forn f1of1 elssuni f1imaen syl2an simpl1 trss 3adant1 sselda fvex adantlr
      imp ensdomtr sseq1 breq1 anbi12d biimprcd rexlimdva ralrimiv cfslb2n ciun
      syl5bi dfiun2 ex ax-17 syl6 com23 expcom ralrimivw iunss sylibr wf foelrn
      fof eluni2 hbiu1 hbel wfun cdm fdm sseq2d syl5ibr jctild funfvima2 ssiun2
      sseld sylcom rexlimd eleq1a rexlimdv sylsyld ssrdv eqssd syl5eqr necon3ai
      ffun pm2.01d nexdv entr bren sylib 3ad2ant1 mtod wo uniss biimpi sylan9ss
      df-tr syld tsken 3impb ord ) BUAGZBUBZABGZUCZAUDZBHIZUEUWIBGZUWHUWJUWIBUF
      UGZCUHZUIZCUJZUWHUWNCUWHUWNUWHUWNUWNUEZUWHUWNJZDUHZUWMEUHZUKZKZEALZDULZUD
      ZUWLUMZUWPUWQUXCUWLUPUGZMIZUXEUWHUXGUWNUWEUWGUXGUWFUWEUWGJZUXCUWLUXFMUXHA
      UWLMIZUXCUWLMIZUXHABMIZBUWLHIZUXIABUNUWEUXLUWGUWEUWLBHIZUXLUWEUWSBHIENLUX
      MEBUOEBUQOUWLBUAURUSZUTUWLNGZUXKUXLJUXIPBVAZABUWLNVBVCQUWGUXIUXJUWEUWGUXI
      UXCARIZUXJUXIUWGUWSAHIENLZUXQUXIAUWSRIZENLZUXRUXIUXOAUWLRIZUXTUXPAUWLVDUX
      SUYAEUWLNUWSUWLARVEVFVGEEAVHOUWGUXRJUXCEAUWTVIZVJZAREDAUWTUYBUYBVKZVLUWGU
      XRAUYCUYBVMZUYCARIUYBAVNZUYEUWTSGZUYFEAEAUWTUYBSUYDVOUYGUWSAGZUWMSGUYGCVP
      UWMUWSSWAVCZVQVRAUYBVSVTEAUYCUYBBWBWCWDWEUXCAUWLWFWGWHWIUXHUWLWJGZUXFUWLK
      ZUWGUWEBWKUMUYJBAWLBWMWEZUYJUWLWKUMUYKUWSWNUWLMIEUWLWOEUWLWPWQZOWRWSUTUWQ
      UWLWTZFUHZUWLTZUYOUXFMIZJZFUXCWOUXGUXEPUWQUYJUWLXBGUYNUWHUYJUWNUWEUWGUYJU
      WFUYLWSUTZUWLXAUWLXCXDUWQUYRFUXCUYOUXCGUYOUWTKZEALZUWQUYRUXBVUADUYOFVPUWR
      UYOKUXAUYTEAUWRUYOUWTXEXFXGUWQUYTUYREAUWQUYHJZUWTUWLTZUWTUXFMIZUYTUYRPUWN
      VUCUWHUYHUWNUWMVJZUWTUWLUWMUWSXHUWNUWIUWLUWMVMZVUEUWLKUWIUWLUWMXIZUWIUWLU
      WMXMOXJZXKVUBUWTUWLUXFMVUBUWTUWSHIZUWSUWLMIZUWTUWLMIUWNUYHVUIUWHUWNUWIUWL
      UWMXLUWSUWITZVUIUYHUWIUWLUWMXNUWSAXOZUWIUWLUWSUWMEVPXPXQWHUWHUYHVUJUWNUWH
      UYHJZUWSBMIZUXLVUJVUMUWEUWSBGVUNUWEUWFUWGUYHXRZUWHABUWSUWFUWGABTZUWEUWFUW
      GVUPBAXSZYDXTYAUWSBUNQVUMUWEUXLVUOUXNOUWLSGVUNUXLJVUJPBUFYBZUWSBUWLSVBVCQ
      YCUWTUWSUWLYEQUWQUYKUYHUWQUYJUYKUYSUYMOUTWRUYTUYRVUCVUDJUYTUYPVUCUYQVUDUY
      OUWTUWLYFUYOUWTUXFMYGYHYIQYJYNYKFUWLUXCVURYLQUSUWNUXDUWLUWNUXDEAUWTYMZUWL
      EDAUWTUYIYOUWNVUSUWLUWNVUCEAWOVUSUWLTUWNVUCEAVUHUUAEAUWTUWLUUBUUCUWNFUWLV
      USUWNVUFUYOUWLGZUYOVUSGZPVUGVUFUWIUWLUWMUUDZVUTUYOUWRUWMUGZKZDUWILZVVAUWI
      UWLUWMUUFVUFVUTVVEDUWIUWLUYOUWMUUEYPVVBVVDVVADUWIVVBUWRUWIGZVVCVUSGZVVDVV
      APVVFUWRUWSGZEALVVBVVGEUWRAUUGVVBVVHVVGEAVVBEYQEFFVVCVUSUYOVVCGEYQEFAUWTU
      UHUUIVVBVVHUYHVVGVVBVVHUYHVVGPVVBVVHJUYHVVCUWTGZVVGVVBVVHUYHVVIPVVBUYHVVH
      VVIVVBUYHUWMUUJZUWSUWMUUKZTZJVVHVVIPVVBUYHVVLVVJUYHVVLVVBVUKVULVVBVVKUWIU
      WSUWIUWLUWMUULUUMUUNUWIUWLUWMUVHUUOUWSUWRUWMUUPYRYSYDUYHUWTVUSVVCEAUWTUUQ
      UURUUSYPYSUUTYNVVCVUSUYOUVAYRUVBUVCOUVDUVEUVFUVGOYPUVIUVJUWEUWFUWJUWOPUWG
      UWJUWEUWOUWJUWEJUWIUWLHIZUWOUWEUWJUXLVVMUXNUWIBUWLUVKWEUWIUWLCVURUVLUVMYT
      UVNUVOUWHUWJUWKUWEUWFUWGUWJUWKUVPZUWFUWGJUWEUWIBTZVVNUWFUWGVVOUWFUWGVUPVV
      OVUQVUPUWFVVOVUPUWFUWIBUDZBABUVQUWFVVPBTBUVTUVRUVSYTUWAYDUWIBUWBWEUWCUWDU
      S $.
      $( [22-Jun-2013] $)
  $}

  $( The intersection of an element of a transitive Tarski's class is an
     element of the class.  (Contributed by FL, 17-Apr-2011.)  (Revised by
     Mario Carneiro, 20-Sep-2014.) $)
  tskint $p |- ( ( ( T e. Tarski /\ Tr T ) /\ A e. T /\ A =/= (/) ) ->
    |^| A e. T ) $=
    ( ctsk wcel wtr wa c0 wne w3a cuni wss simp1l tskuni 3expa 3adant3 intssuni
    cint 3ad2ant3 tskss syl3anc ) BCDZBEZFZABDZAGHZIUAAJZBDZAQZUFKZUHBDUAUBUDUE
    LUCUDUGUEUAUBUDUGABMNOUEUCUIUDAPRUFUHBST $.
    $( [20-Sep-2014] $) $( [17-Apr-2011] $)

  $( The union of two elements of a transitive Tarski's class is in the set.
     (Contributed by Mario Carneiro, 20-Sep-2014.) $)
  tskun $p |- ( ( ( T e. Tarski /\ Tr T ) /\ A e. T /\ B e. T ) ->
    ( A u. B ) e. T ) $=
    ( ctsk wcel wtr wa w3a cpr cuni cun wceq uniprg 3adant1 simp1l simp1r tskpr
    3adant1r tskuni syl3anc eqeltrrd ) CDEZCFZGZACEZBCEZHZABIZJZABKZCUEUFUIUJLU
    DABCCMNUGUBUCUHCEZUICEUBUCUEUFOUBUCUEUFPUBUEUFUKUCABCQRUHCSTUA $.
    $( [20-Sep-2014] $)

  $( The cross product of two elements of a transitive Tarski's class is an
     element of the class.  JFM CLASSES2 th. 67 (partly).  (Contributed by FL,
     15-Apr-2011.)  (The proof was shortened by Mario Carneiro,
     20-Sep-2014.) $)
  tskxp $p |- ( ( ( T e. Tarski /\ Tr T ) /\ A e. T /\ B e. T )
  -> ( A X. B ) e. T ) $=
    ( ctsk wcel wtr w3a cun cxp simp1l tskun cpw tskpw wss xpsspw mp3an3 syldan
    wa tskss syl2anc ) CDEZCFZRACEZBCEZGUAABHZCEZABIZCEZUAUBUCUDJABCKUAUFUELZCE
    ZUHUECMUAUJUILZCEZUHUICMUAULUGUKNUHABOUKUGCSPQQT $.
    $( [20-Sep-2014] $) $( [15-Apr-2011] $)

  $( Set exponentiation is an element of a transitive Tarski's class.  JFM
     CLASSES2 th. 67 (partly).  (Contributed by FL, 15-Apr-2011.)  (The proof
     was shortened by Mario Carneiro, 20-Sep-2014.) $)
  tskmap $p |- ( ( ( T e. Tarski /\ Tr T ) /\ A e. T /\ B e. T )
  -> ( A ^m B ) e. T ) $=
    ( ctsk wcel wtr wa w3a cxp cpw cmap wss simp1l tskxp 3com23 syl2anc mapsspw
    co tskpw 3ad2ant3 tskss syl3anc ) CDEZCFZGZACEZBCEZHZUCBAIZJZCEZABKRZUJLZUL
    CEUCUDUFUGMZUHUCUICEZUKUNUEUGUFUOBACNOUICSPUGUEUMUFABCQTUJULCUAUB $.
    $( [20-Sep-2014] $) $( [15-Apr-2011] $)

  ${
    $d A x y z $.  $d B x y z $.  $d T x y z $.
    $( A transitive Tarski's class is closed under small unions.  (Contributed
       by Mario Carneiro, 22-Jun-2013.) $)
    tskurn $p |- ( ( ( T e. Tarski /\ Tr T ) /\ A e. T /\ F : A --> T )
    -> U. ran F e. T ) $=
      ( vx vy ctsk wcel wss csdm wbr 3ad2ant3 cv cen con0 wrex wwe sylc syl3anc
      wex ween wtr wa w3a crn cuni simp1l simp1r frn cdom wfo simp2 trss tskwe2
      wf mpbird wess eximdv syl5com syl mpbid wfn dffn4 sylib fodomnumg tsksdom
      wb ffn syl2anc domsdomtr tskssel tskuni ) BFGZBUAZUBZABGZABCUNZUCZVLVMCUD
      ZBGZVRUEBGVLVMVOVPUFZVLVMVOVPUGZVQVLVRBHZVRBIJZVSVTVPVNWBVOABCUHKVQVRAUIJ
      ZABIJZWCVQVODLZAMJDNOZAVRCUJZWDVNVOVPUKZVQAELZPZESZWGVQVLABHZWLVTVQVMVOWM
      WAWIBAULQVLBWJPZESZWMWLVLWOWFBMJDNODBUMDBFETUOWMWNWKEABWJUPUQURQVQVOWLWGV
      FWIDABETUSUTVPVNWHVOVPCAVAWHABCVGACVBVCKDAVRCBVDRVQVLVOWEVTWIABVEVHVRABVI
      VHVRBVJRVRBVKR $.
      $( [22-Jun-2013] $)
  $}

  $( Two ways of saying a class of ordinals is unbounded.  (Contributed by
     Mario Carneiro, 8-Jun-2013.) $)
  ssonprc $p |- ( A C_ On -> ( A e/ _V <-> U. A = On ) ) $=
    ( cvv wnel wcel wn con0 wss cuni wceq df-nel word ssorduni ordeleqon orcomd
    wo sylib ord elex uniexb sylibr syl6 con1d onprc uniexg syl5ib mtoi impbid1
    eleq1 syl5bb ) ABCABDZEZAFGZAHZFIZABJULUKUNULUNUJULUNEUMFDZUJULUNUOULUOUNUL
    UMKUOUNOALUMMPNQUOUMBDZUJUMFRASTUAUBUNUJFBDZUCUJUPUNUQABUDUMFBUHUEUFUGUI $.
    $( [8-Jun-2013] $)

