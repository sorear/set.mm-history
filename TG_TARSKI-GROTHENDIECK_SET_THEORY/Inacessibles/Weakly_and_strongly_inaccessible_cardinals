$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Weakly and strongly inaccessible cardinals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c InaccW $. $( Weakly inaccessible $)
  $c Inacc $. $( Strongly inaccessible $)

  $( The class of weak inaccessibles. $)
  cwina $a class InaccW $.

  $( The class of strong inaccessibles. $)
  cina $a class Inacc $.

  ${
    $d A x y z $.
    $( An ordinal is weakly inaccessible iff it is a regular limit cardinal.
       Note that our definition allows ` om ` as a weakly inacessible
       cardinal. $)
    df-wina $a |- InaccW = { x | ( x =/= (/) /\ ( cf ` x ) = x /\
      A. y e. x E. z e. x y ~< z ) } $.

    $( An ordinal is strongly inaccessible iff it is a regular strong limit
       cardinal, which is to say that it dominates the powersets of every
       smaller ordinal. $)
    df-ina $a |- Inacc = { x | ( x =/= (/) /\ ( cf ` x ) = x /\
      A. y e. x ~P y ~< x ) } $.
  $}

  ${
    $d A x y z $.
    $( Conditions of weak inaccessibility. $)
    elwina $p |- ( A e. InaccW <-> ( A =/= (/) /\ ( cf ` A ) = A /\
        A. x e. A E. y e. A x ~< y ) ) $=
      ( vz cwina wcel cvv c0 wne ccf cfv wceq csdm wbr wrex wral w3a elex fvex
      cv eleq1 mpbii 3ad2ant2 neeq1 wb fveq2 mpancom rexeq raleqbi1dv 3anbi123d
      eqeq12 df-wina elab2g pm5.21nii ) CEFCGFZCHIZCJKZCLZATBTMNZBCOZACPZQZCERU
      RUPUOVAURUQGFUOCJSUQCGUAUBUCDTZHIZVCJKZVCLZUSBVCOZAVCPZQVBDCEGVCCLZVDUPVF
      URVHVAVCCHUDVEUQLVIVFURUEVCCJUFVEUQVCCUKUGVGUTAVCCUSBVCCUHUIUJDABULUMUN
      $.
      $( [22-Jun-2013] $)

    $( Conditions of strong inaccessibility. $)
    elina $p |- ( A e. Inacc <-> ( A =/= (/) /\ ( cf ` A ) = A /\
      A. x e. A ~P x ~< A ) ) $=
      ( vy cina wcel cvv c0 wne ccf cfv wceq cv cpw csdm wbr wral w3a elex fvex
      eleq1 mpbii 3ad2ant2 neeq1 wb fveq2 eqeq12 mpancom breq2 3anbi123d df-ina
      raleqbi1dv elab2g pm5.21nii ) BDEBFEZBGHZBIJZBKZALMZBNOZABPZQZBDRUQUOUNUT
      UQUPFEUNBISUPBFTUAUBCLZGHZVBIJZVBKZURVBNOZAVBPZQVACBDFVBBKZVCUOVEUQVGUTVB
      BGUCVDUPKVHVEUQUDVBBIUEVDUPVBBUFUGVFUSAVBBVBBURNUHUKUICAUJULUM $.
      $( [22-Jun-2013] $)
  $}

  ${
    $d A x y $.
    $( A weakly inaccessible cardinal is an ordinal. $)
    winaon $p |- ( A e. InaccW -> A e. On ) $=
      ( vx vy cwina wcel c0 wne ccf cfv wceq csdm wbr wrex wral w3a con0 elwina
      cv cfon eleq1 mpbii 3ad2ant2 sylbi ) ADEAFGZAHIZAJZBRCRKLCAMBANZOAPEZBCAQ
      UFUDUHUGUFUEPEUHASUEAPTUAUBUC $.
      $( [29-May-2013] $)

    $( Lemma for ~ inawina . $)
    inawinalem $p |- ( A e. On ->
        ( A. x e. A ~P x ~< A -> A. x e. A E. y e. A x ~< y ) ) $=
      ( con0 wcel cv cpw csdm wbr wrex wa cen cdom sdomdom breq2 rcla4ev wi mpd
      vex cvv sylan2 ondomen syl ensdomtr ad2ant2l sdomel ad2ant2r canth2 ensym
      pwex sdomentr ax-mp sylancr ad2antlr jca expcom reximdv2 ex ralimdv ) CDE
      ZAFZGZCHIZVABFZHIZBCJZACUTVCVFUTVCKZVDVBLIZBDJZVFVGVBVDMIZBDJZVIVCUTVBCMI
      ZVKVBCNVJVLBCDVDCVBMOPUABBVBUBUCVGVHVEBDCVDDEZVHKZVGVDCEZVEKVNVGKZVOVEVPV
      DCHIZVOVHVCVQVMUTVDVBCUDUEVMUTVQVOQVHVCVDCUFUGRVHVEVMVGVHVAVBHIZVBVDLIZVE
      VAASZUHVDVBVAVTUJUIVDTEVRVSKVEQBSVAVBVDTUKULUMUNUOUPUQRURUS $.
      $( [8-Jun-2013] $)

    $( Every strongly inaccessible cardinal is weakly inaccessible. $)
    inawina $p |- ( A e. Inacc -> A e. InaccW ) $=
      ( vx vy c0 wne ccf cfv wceq cv cpw csdm wbr wral w3a wrex cina wcel cwina
      con0 idd cfon eleq1 mpbii inawinalem 3anim123d mpcom elina elwina 3imtr4i
      3ad2ant2 ) ADEZAFGZAHZBIZJAKLBAMZNZUKUMUNCIKLCAOBAMZNZAPQARQASQZUPURUMUKU
      SUOUMULSQUSAUAULASUBUCUJUSUKUKUMUMUOUQUSUKTUSUMTBCAUDUEUFBAUGBCAUHUI $.
      $( [29-May-2013] $)
  $}

  ${
    $( ` om ` is a strongly inaccessible cardinal.  (Many definitions of
       "inaccessible" explicitly disallow ` om ` as an inaccessible cardinal,
       but this choice allows us to reuse our results for inaccessibles for
       ` om ` .) $)
    omina $p |- om e. Inacc $=
      ( vx com cina wcel c0 wne ccf cfv wceq cv cpw csdm wral peano1 ne0i ax-mp
      wbr cfom cfn sylib ssid ssnnfi mpan2 pwfi isfinite3 rgen elina mpbir3an
      wss ) BCDBEFZBGHBIAJZKZBLQZABMEBDUJNBEOPRUMABUKBDZULSDZUMUNUKSDZUOUNUKUKU
      IUPUKUAUKUKUBUCUKUDTULUETUFABUGUH $.
      $( [29-May-2013] $)
  $}

  ${
    $d A x y $.
    $( A weakly inaccessible cardinal is a cardinal. $)
    winacard $p |- ( A e. InaccW -> ( card ` A ) = A ) $=
      ( vx vy cwina wcel c0 wne ccf cfv wceq csdm wbr wrex wral w3a ccrd elwina
      cv cardcf fveq2 id eqeq12d mpbii 3ad2ant2 sylbi ) ADEAFGZAHIZAJZBRCRKLCAM
      BANZOAPIZAJZBCAQUHUFUKUIUHUGPIZUGJUKASUHULUJUGAUGAPTUHUAUBUCUDUE $.
      $( [29-May-2013] $)
  $}

  ${
    $d A w x y z $.
    $( A weakly inaccessible cardinal is infinite. $)
    winainflem $p |- ( ( A =/= (/) /\ A e. On /\
        A. x e. A E. y e. A x ~< y ) -> om C_ A ) $=
      ( vz vw c0 con0 wcel cv csdm wbr wrex w3a com wss wn wceq wa wi syl simp1
      wne wral csuc nn0suc necon2bi vex sucid eleq2 mpbiri adantl breq1 rexbidv
      wo weq breq2 cbvrexv syl6bb rcla4v cdom biimpa 3ad2antl2 wb nnon suceloni
      eleq1 sylan 3adant3 onelon simpl1 onsssuc syl2anc mpbird cvv ssdomg ax-mp
      biimparc domnsym 3syl nrexdv pm2.65d simp3 nsyl rexlimiva jaoi con2i word
      3expia ordom eloni 3ad2ant2 ordtri1 sylancr ) CFUBZCGHZAIZBIZJKZBCLZACUCZ
      MZNCOZCNHZPZXCXAXCCFQZCDIZUDZQZDNLZUNXAPZDCUEXEXJXIXACFWNWOWTUAUFXHXJDNXF
      NHZXHRZWTXAXLWTXFEIZJKZECLZXLXFCHZWTXOSXHXPXKXHXPXFXGHXFDUGUHCXGXFUIUJUKW
      SXOAXFCADUOZWSXFWQJKZBCLXOXQWRXRBCWPXFWQJULUMXRXNBECWQXMXFJUPUQURUSTXKXHW
      TXOPXKXHWTMZXNECXSXMCHZRZXMXFOZXMXFUTKZXNPYAYBXMXGHZXHXKXTYDWTXHXTYDCXGXM
      UIVAVBYAXMGHZXFGHZYBYDVCXSWOXTYEXKXHWOWTXKXGGHZXHWOXKYFYGXFVDZXFVETXHWOYG
      CXGGVFVQVGVHCXMVIVGYAXKYFXKXHWTXTVJYHTXMXFVKVLVMXMVNHYBYCSEUGXMXFVNVOVPXM
      XFVRVSVTWHWAWNWOWTWBWCWDWETWFXANWGCWGZXBXDVCWIWOWNYIWTCWJWKNCWLWMVM $.
      $( [29-May-2013] $)

    $( A weakly inaccessible cardinal is infinite. $)
    winainf $p |- ( A e. InaccW -> om C_ A ) $=
      ( vx vy cwina wcel wne ccf cfv wceq csdm wbr wrex wral w3a com wss elwina
      c0 cv con0 cfon eleq1 mpbii winainflem syl3an2 sylbi ) ADEARFZAGHZAIZBSCS
      JKCALBAMZNOAPZBCAQUIUGATEZUJUKUIUHTEULAUAUHATUBUCBCAUDUEUF $.
      $( [29-May-2013] $)

    $( A weakly inaccessible cardinal is a limit ordinal. $)
    winalim $p |- ( A e. InaccW -> Lim A ) $=
      ( cwina wcel com wss wlim winainf ccrd cfv wceq wb winacard cardlim sseq2
      limeq bibi12d mpbii syl mpbid ) ABCZDAEZAFZAGTAHIZAJZUAUBKZALUDDUCEZUCFZK
      UEAMUDUFUAUGUBUCADNUCAOPQRS $.
      $( [29-May-2013] $)

    $( A nontrivial weakly inaccessible cardinal is a limit aleph. $)
    winalim2 $p |- ( ( A e. InaccW /\ A =/= om ) ->
      E. x ( ( aleph ` x ) = A /\ Lim x ) ) $=
      ( vy vw vz wcel com wne wa cv cale cfv wceq con0 wrex wex c0 wn csdm wbr
      cwina wlim ccrd winacard wss winainf cardalephex syl adantr df-rex simprr
      wb mpbid eqcomd csuc cvv w3o simprl onzsl sylib simplr aleph0 syl6eq eqtr
      fveq2 sylan2 ex necon3ad sylc pm2.21d wral suceloni sucid alephord2i ee10
      vex ad2antrl simplrr ad2antll eqtrd eleqtrrd ccf elwina simp3bi ad3antrrr
      breq1 rexbidv rcla4va syl2anc expr wi iscard simprbi ra4 3syl alephnbtwn2
      breq2d sylibd pm3.21 mtoi syl6 imp nrexdv pm2.65d simpr a1i 3jaod mpd jca
      eximdv syl5bi ) BUAFZBGHZIZBAJZKLZMZANOZXPBMZXOUBZIZAPZXLXRXMXLBUCLBMZXRB
      UDZXLGBUEYCXRULBUFABUGUHUMUIXRXONFZXQIZAPXNYBXQANUJXNYFYAAXNYFYAXNYFIZXSX
      TYGBXPXNYEXQUKZUNYGXOQMZXOCJZUOZMZCNOZXOUPFZXTIZUQZXTYGYEYPXNYEXQURCXOUSU
      TYGYIXTYMYOYGYIXTYGXQXMYIRYHXLXMYFVAXQYIBGXQYIBGMZYIXQXPGMYQYIXPQKLGXOQKV
      EVBVCBXPGVDVFVGVHVIVJYGYMXTYGYLCNYGYJNFZIYLYJKLZDJZSTZDBOZYGYRYLUUBYGYRYL
      IZIZYSBFEJZYTSTZDBOZEBVKZUUBUUDYSYKKLZBYRYSUUIFZYGYLYRYKNFYJYKFUUJYJVLYJC
      VPVMYJYKVNVOVQUUDBXPUUIXNYEXQUUCVRYLXPUUIMYGYRXOYKKVEVSVTZWAXLUUHXMYFUUCX
      LBQHBWBLBMUUHEDBWCWDWEUUGUUBEYSBUUEYSMUUFUUADBUUEYSYTSWFWGWHWIWJYGYRYLUUB
      RUUDUUADBUUDYTBFZUUARZUUDUULYTUUISTZUUMUUDUULYTBSTZUUNXLUULUUOWKZXMYFUUCX
      LYCUUODBVKZUUPYDYCBNFUUQDBWLWMUUODBWNWOWEUUDBUUIYTSUUKWQWRUUNUUAUUAUUNIYJ
      YTWPUUNUUAWSWTXAXBXCWJXDXCVJYOXTWKYGYNXTXEXFXGXHXIVGXJXKXH $.
      $( [29-May-2013] $)
  $}

  ${
    $d A x $.  $d A y z $.
    $( A nontrivial weakly inaccessible cardinal is a fixed point of the aleph
       function. $)
    winafp $p |- ( ( A e. InaccW /\ A =/= om ) -> ( aleph ` A ) = A ) $=
      ( vx vy vz cwina wcel com wne cale cfv wceq wlim wex wss cvv ad2antll ccf
      wa cv fveq2d winalim2 con0 vex limelon mpan alephle syl sseqtrd alephsing
      simprl c0 csdm wbr wrex wral elwina simp2bi ad2antrr 3eqtr3d a1i eqsstr3d
      cfle eqssd eqtr3d ex exlimdv mpd ) AEFZAGHZRZBSZIJZAKZVKLZRZBMAIJZAKZBAUA
      VJVOVQBVJVOVQVJVORZVLVPAVRVKAIVRVKAVRVKVLAVNVKVLNZVJVMVNVKUBFZVSVKOFVNVTB
      UCVKOUDUEVKUFUGPVJVMVNUJZUHVRAVKQJZVKVRVLQJZAQJZWBAVRVLAQWATVNWCWBKVJVMVK
      UIPVHWDAKZVIVOVHAUKHWECSDSULUMDAUNCAUOCDAUPUQURUSWBVKNVRVKVBUTVAVCTWAVDVE
      VFVG $.
      $( [29-May-2013] $)
  $}

  ${
    winafp.1 $e |- A e. InaccW $.
    winafp.2 $e |- A =/= om $.
    $( This theorem, which states that a nontrivial inaccessible cardinal is
       its own aleph number, is stated here in inference form, where the
       assumptions are in the hypotheses rather than an antecedent.  Often, we
       use ~ dedth to turn this type of statement into the closed form
       statement ~ winafp , but in this case, since it is consistent with ZFC
       that there are no nontrivial inaccessible cardinals, it is not possible
       to prove ~ winafp using this theorem and ~ dedth , in ZFC. (You can
       prove this if you use ~ ax-groth , though.)  (Contributed by Mario
       Carneiro, 28-May-2014.) $)
    winafpi $p |- ( aleph ` A ) = A $=
      ( cwina wcel com wne cale cfv wceq winafp mp2an ) ADEAFGAHIAJBCAKL $.
      $( [28-May-2014] $)
  $}

