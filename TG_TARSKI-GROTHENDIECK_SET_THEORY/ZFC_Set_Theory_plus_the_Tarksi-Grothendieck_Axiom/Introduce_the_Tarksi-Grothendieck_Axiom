$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Introduce the Tarksi-Grothendieck Axiom
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u f $.
    $( The Tarksi-Grothendieck Axiom.  For every set ` x ` there is an
       inaccessible cardinal ` y ` such that ` y ` is not in ` x ` .  The
       addition of this axiom to ZFC set theory provides a framework for
       category theory, thus for all practical purposes giving us a complete
       foundation for "all of mathematics."  This version of the axiom is used
       by the Mizar project
       ( ~ http://www.mizar.org/JFM/Axiomatics/tarski.html ).  Unlike the ZFC
       axioms, this axiom is very long when expressed in terms of primitive
       symbols - see ~ grothprim .  An open problem is finding a shorter
       equivalent. $)
    ax-groth $a |- E. y ( x e. y /\ A. z e. y ( A. w ( w C_ z -> w e. y ) /\
                       E. w e. y A. v ( v C_ z -> v e. w ) ) /\
                     A. z ( z C_ y -> ( z ~~ y \/ z e. y ) ) ) $.

    $( The Tarski-Grothendieck axiom using abbreviations. $)
    axgroth5 $p |- E. y ( x e. y /\ A. z e. y ( ~P z C_ y
             /\ E. w e. y ~P z C_ w ) /\ A. z e. ~P y ( z ~~ y \/ z e. y ) ) $=
      ( vv cv wcel cpw wss wrex wa wral cen wbr wo w3a wex wi wal pwss ax-groth
      biid rexbii anbi12i ralbii df-ral elpw imbi1i albii bitri 3anbi123i exbii
      vex mpbir ) AFBFZGZCFZHZUOIZURDFZIZDUOJZKZCUOLZUQUOMNUQUOGOZCUOHZLZPZBQUP
      UTUQIUTUOGRDSZEFZUQIVJUTGRESZDUOJZKZCUOLZUQUOIZVERZCSZPZBQABCDEUAVHVRBUPU
      PVDVNVGVQUPUBVCVMCUOUSVIVBVLDUQUOTVAVKDUOEUQUTTUCUDUEVGUQVFGZVERZCSVQVECV
      FUFVTVPCVSVOVEUQUOCUMUGUHUIUJUKULUN $.
      $( [22-Jun-2009] $)

    $( Alternate version of the Tarksi-Grothendieck Axiom. $)
    axgroth2 $p |- E. y ( x e. y /\ A. z e. y ( A. w ( w C_ z -> w e. y ) /\
                       E. w e. y A. v ( v C_ z -> v e. w ) ) /\
                     A. z ( z C_ y -> ( y ~<_ z \/ z e. y ) ) ) $=
      ( cv wcel wss wi wal wrex wa cdom wbr wo w3a wex cvv vex ax-mp cen ssdomg
      wral ax-groth biantrurd wb sbthbg syl6bb orbi1d pm5.74i albii exbii mpbir
      3anbi3i ) AFBFZGZDFZCFZHUQUOGIDJEFZURHUSUQGIEJDUOKLCUOUCZURUOHZUOURMNZURU
      OGZOZIZCJZPZBQUPUTVAURUOUANZVCOZIZCJZPZBQABCDEUDVGVLBVFVKUPUTVEVJCVAVDVIV
      AVBVHVCVAVBURUOMNZVBLZVHVAVMVBURRGVAVMICSURUORUBTUEUORGVNVHUFBSURUORUGTUH
      UIUJUKUNULUM $.
      $( [18-Mar-2007] $)

