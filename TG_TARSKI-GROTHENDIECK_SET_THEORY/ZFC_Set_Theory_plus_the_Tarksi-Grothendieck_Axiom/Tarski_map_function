$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Tarski map function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c tarskiMap $.
  $( A function whose value is the smallest Tarski's class
     containing a particular set. $)

  $( Extend class definition to include the map whose value is the smallest
     Tarski's class. $)
  ctskm $a class tarskiMap $.

  ${
    $d x y $.
    $( A function that maps a set ` x ` to the smallest Tarski's class that
       contains the set. $)
    df-tskm $a |- tarskiMap = ( x e. _V |-> |^| { y e. Tarski | x e. y } ) $.
  $}

  ${
    $d A x y $.
    $( Value of our tarski map.  (Contributed by FL, 30-Dec-2010.) $)
    tskmval $p |- ( A e. V ->
      ( tarskiMap ` A ) = |^| { x e. Tarski | A e. x } ) $=
      ( vy wcel cvv ctsk crab cint ctskm wceq elex wrex cuni grothtsk syl6eleqr
      cv cfv eluni2 sylib intexrab eleq1 rabbidv inteqd df-tskm fvmptg syl2anc
      ) BCEZBFEBAQZEZAGHZIZFEZBJRULKBCLZUHUJAGMZUMUHBGNZEUOUHBFUPUNOPABGSTUJAGU
      ATDBDQZUIEZAGHZIULFFJUQBKZUSUKUTURUJAGUQBUIUBUCUDDAUEUFUG $.
      $( [20-Sep-2014] $) $( [30-Dec-2010] $)

    $( The set ` A ` is an element of the smallest Tarski's class that contains
       ` A ` .  CLASSES1 th. 5.  (Contributed by FL, 30-Dec-2010.)  (The proof
       was shortened by Mario Carneiro, 21-Sep-2014.) $)
    tskmid $p |- ( A e. V -> A e. ( tarskiMap ` A ) ) $=
      ( vx wcel cv ctsk crab cint ctskm cfv wi wral id elintrabg mpbiri tskmval
      rgenw eleqtrrd ) ABDZAACEDZCFGHZAIJSAUADTTKZCFLUBCFTMQTCAFBNOCABPR $.
      $( [21-Sep-2014] $) $( [30-Dec-2010] $)

    $( A Tarski's class that contains ` A ` is a Tarski's class.  (Contributed
       by FL, 17-Apr-2011.)  (The proof was shortened by Mario Carneiro,
       21-Sep-2014.) $)
    tskmcl $p |- ( tarskiMap ` A ) e. Tarski $=
      ( vx cvv wcel ctskm cfv ctsk cv crab cint tskmval wss c0 ssrab2 wrex cuni
      wne id grothtsk syl6eleqr eluni2 sylib rabn0 sylibr sylancr eqeltrd fvprc
      inttsk wn 0tsk syl6eqel pm2.61i ) ACDZAEFZGDUMUNABHDZBGIZJZGBACKUMUPGLUPM
      QZUQGDUOBGNUMUOBGOZURUMAGPZDUSUMACUTUMRSTBAGUAUBUOBGUCUDUPUHUEUFUMUIUNMGA
      EUGUJUKUL $.
      $( [21-Sep-2014] $) $( [17-Apr-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Being a part of ` ( tarskiMap `` A ) ` .  (Contributed by FL,
       17-Apr-2011.)  (The proof was shortened by Mario Carneiro,
       20-Sep-2014.) $)
    sstskm $p |- ( A e. V -> ( B C_ ( tarskiMap ` A ) <->
      A. x e. Tarski ( A e. x -> B C_ x ) ) ) $=
      ( wcel ctskm cfv wss cv ctsk cab cint wral crab tskmval df-rab inteqi wal
      wa wi syl6eq sseq2d impexp albii ssintab df-ral 3bitr4i syl6bb ) BDEZCBFG
      ZHCAIZJEZBUKEZSZAKZLZHZUMCUKHZTZAJMZUIUJUPCUIUJUMAJNZLUPABDOVAUOUMAJPQUAU
      BUNURTZARULUSTZARUQUTVBVCAULUMURUCUDUNACUEUSAJUFUGUH $.
      $( [20-Sep-2014] $) $( [17-Apr-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d V y $.  $d W y $.
    $( Belonging to ` ( tarskiMap `` A ) ` .  (Contributed by FL,
       17-Apr-2011.)  (The proof was shortened by Mario Carneiro,
       21-Sep-2014.) $)
    eltskm $p |- ( A e. V -> ( B e. ( tarskiMap ` A ) <->
      A. x e. Tarski ( A e. x -> B e. x ) ) ) $=
      ( wcel ctskm cfv cv ctsk crab cint wi wral tskmval eleq2d cvv elex tskmid
      a1i eleq2 tskmcl wceq imbi12d rcla4v ax-mp syl5com wb elintrabg pm5.21ndd
      syl6 bitrd ) BDEZCBFGZEZCBAHZEZAIJKZEZUPCUOEZLZAIMZULUMUQCABDNOULCPEZURVA
      URVBLULCUQQSULVAUNVBULBUMEZVAUNBDRUMIEVAVCUNLZLBUAUTVDAUMIUOUMUBUPVCUSUNU
      OUMBTUOUMCTUCUDUEUFCUMQUJVBURVAUGLULUPACIPUHSUIUK $.
      $( [21-Sep-2014] $) $( [17-Apr-2011] $)
  $}

$( End of set.mm $)

$( The work below may eventually be broken off as a separate
   module, dedekind.mm. $)

