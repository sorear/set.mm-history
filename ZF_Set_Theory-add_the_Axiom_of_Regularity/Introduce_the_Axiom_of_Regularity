$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Introduce the Axiom of Regularity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Axiom of Regularity.  An axiom of Zermelo-Fraenkel set theory.  Also
       called the Axiom of Foundation.  A rather non-intuitive axiom that
       denies more than it asserts, it states (in the form of ~ zfreg ) that
       every non-empty set contains a set disjoint from itself.  One
       consequence is that it denies the existence of a set containing itself
       ( ~ eirrv ).  A stronger version that works for proper classes is proved
       as ~ zfregs . $)
    ax-reg $a |- ( E. y y e. x ->
                 E. y ( y e. x /\ A. z ( z e. y -> -. z e. x ) ) ) $.

    $( Axiom of Regularity expressed more compactly. $)
    axreg $p |- ( x e. y ->
                 E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) $=
      ( cv wcel wn wi wal wa wex ax-reg 19.23bi ) ADZBDZEZOCDZMEPNEFGCHIAJABACK
      L $.
      $( [14-Aug-2003] $)
  $}

  ${
    $d x y A z $.
    zfregcl.1 $e |- A e. V $.
    $( The Axiom of Regularity with class variables. $)
    zfregcl $p |- ( E. x x e. A -> E. x e. A A. y e. x -. y e. A ) $=
      ( vz cv wcel wex wn wral wrex wi wceq eleq2 exbidv negbid ralbidv rexeqd
      imbi12d hbre1 wal wa axreg df-ral rexbii df-rex bitr2 sylib 19.23ai
      vtocl ) AFZEFZGZAHZBFZULGZIZBUKJZAULKZLUKCGZAHZUOCGZIZBUKJZACKZLECDULCMZU
      NVAUSVEVFUMUTAULCUKNOURVDAULCVFUQVCBUKVFUPVBULCUONPQRSUMUSAURAULTUMUMUOUK
      GUQLBUAZUBAHZUSAEBUCUSVGAULKVHURVGAULUQBUKUDUEVGAULUFUGUHUIUJ $.
      $( [7-May-2004] $) $( [5-Aug-1994] $)
  $}

  ${
    $d x A y $.
    zfreg.1 $e |- A e. V $.
    $( The Axiom of Regularity using abbreviations.  Axiom 6 of [TakeutiZaring]
       p. 21.  This is called the "weak form."  There is also a
       "strong form," not requiring that ` A ` be a set, that can be proved
       with more difficulty (see ~ zfregs ).  $)
    zfreg $p |- ( A =/= (/) -> E. x e. A ( x i^i A ) = (/) ) $=
      ( vy cv wcel wex wn wral wrex c0 wne cin wceq zfregcl ne0 disj rexbii
      3imtr4 ) AEZBFAGDEBFHDTIZABJBKLTBMKNZABJADBCOABPUBUAABDTBQRS $.
      $( [11-Apr-2007] $) $( [26-Nov-1995] $)
  $}

  ${
    $d x A y $.
    zfreg2.1 $e |- A e. V $.
    $( The Axiom of Regularity using abbreviations.  This form with the
       intersection arguments commuted (compared to ~ zfreg ) is formally
       more convenient for us in some cases.  Axiom Reg of [BellMachover]
       p. 480. $)
    zfreg2 $p |- ( A =/= (/) -> E. x e. A ( A i^i x ) = (/) ) $=
      ( vy cv wcel wex wn wral wrex c0 wne cin wceq zfregcl ne0 incom eqeq1i
      disj bitr rexbii 3imtr4 ) AEZBFAGDEBFHDUCIZABJBKLBUCMZKNZABJADBCOABPUFUDA
      BUFUCBMZKNUDUEUGKBUCQRDUCBSTUAUB $.
      $( [12-Apr-2007] $) $( [17-Sep-2003] $)
  $}

  ${
    $d x y z $.
    $( The membership relation is irreflexive:  no set is a member of itself.
       Theorem 105 of [Suppes] p. 54.  (This is trivial to prove from ~ zfregfr
       and ~ efrirr , but this proof is direct from the Axiom of
       Regularity.) $)
    eirrv $p |- -. x e. x $=
      ( vz vy cv wcel csn wn wral wrex wex eleq1 visset snid a4w1 snex zfregcl
      ax-mp wceq wi ax-14 equcoms com12 elsn syl5ib negbid rcla4cv mt2i nsyli
      con2d r19.21aiv ralnex sylib mt2 ) ADZUNEZBDZUNFZEZGZBCDZHZCUQIZUTUQEZCJV
      BVCUNUQEZCAUTUNUQKUNALMZNCBUQUNOPQUOVAGZCUQHVBGUOVFCUQUOVAVCUOVCUNUTEZVAU
      OUTUNRZVGVCVHUOVGUOVGSACACATUAUBCUNUCUDVAVGVDVEUSVDGBUNUTUPUNRURVDUPUNUQK
      UEUFUGUHUIUJVACUQUKULUM $.
      $( [19-Aug-1993] $)
  $}

  ${
    $d A x $.
    $( No class is a member of itself.  Exercise 6 of [TakeutiZaring] p. 22. $)
    eirr $p |- -. A e. A $=
      ( vx wcel wn wi cv wceq eleq2 eleq1 bitrd negbid eirrv vtoclg pm2.01
      ax-mp ) AACZPDZEQBFZRCZDQBAARAGZSPTSRACPRARHRAAIJKBLMPNO $.
      $( [7-Aug-1994] $)

    $( A class is equal to its successor iff it is a proper class (assuming the
       Axiom of Regularity). $)
     sucprcreg $p |- ( -. A e. V <-> suc A = A ) $=
       ( vx cvv wcel wn csuc wceq sucprc cv wi wal eirr ax-17 eleq1 ceqsalg
       mtbiri csn wo cun wss ssid wb df-suc eqeq1i sseq1 sylbi mpbiri sseld
       elun syl5ibr olc syl5 elsn 19.21aiv nsyl3 impbi ) ACDZEAFZAGZAHUQBIZAGZU
       TADZJZBKZUSUQVDAADZALVBVEBACVEBMUTAANOPUSVCBUSUTAQZDZVBVAUSVBVGRZVBVGUSU
       TAVFSZDVBVHUSVIAUTUSVIATZAATZAUAUSVIAGVJVKUBURVIAAUCUDVIAAUEUFUGUHUTAVFU
       IUJVGVBUKULBAUMUJUNUOUP $.
       $( [6-Jan-2004] $) $( [9-Jul-2004] $)
  $}

  ${
    $d x y A $.
    $( The epsilon relation is founded on any class. $)
    zfregfr $p |- E Fr A $=
      ( vx vy cep wfr cv wss c0 wne wa cin wceq wrex wi dfepfr visset zfreg2
      adantl mpgbir ) ADEBFZAGZTHIZJTCFKHLCTMZNBBCAOUBUCUACTBPQRS $.
      $( [26-Nov-1995] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( No class has 2-cycle membership loops.  Theorem 7X(b) of [Enderton]
       p. 206. $)
    en2lp $p |- -. ( A e. B /\ B e. A ) $=
      ( vx vy cvv wcel wa wn cv wceq eleq1 eleq2 anbi12d negbid cep wfr
      zfregfr visset pm3.2i efrn2lp mp2an vtocl2g elisset anim12i con3i
      pm2.61i ) AEFZBEFZGZABFZBAFZGZHZCIZDIZFZUOUNFZGZHZAUOFZUOAFZGZHUMCDABEEUN
      AJZURVBVCUPUTUQVAUNAUOKUNAUOLMNUOBJZVBULVDUTUJVAUKUOBALUOBAKMNEOPUNEFZUOE
      FZGUSEQVEVFCRDRSCDETUAUBULUIUJUGUKUHABUCBAUCUDUEUF $.
      $( [16-Oct-1996] $)

  $}

  ${
    preleq.1 $e |- A e. V $.
    preleq.2 $e |- B e. V $.
    preleq.3 $e |- C e. V $.
    preleq.4 $e |- D e. V $.
    $( Equality of two unordered pairs when one member of each pair contains
       the other member. $)
    preleq $p |- ( ( ( A e. B /\ C e. D ) /\ { A , B } = { C , D } ) ->
                   ( A = C /\ B = D ) ) $=
      ( cpr wceq wcel wa wn wo preq12b biimp ord en2lp eleq12 anbi1d mtbiri
      syl6 a3d impcom ) ABICDIJZABKZCDKZLZACJBDJLZUEUIUHUEUIMADJBCJLZUHMUEUIUJU
      EUIUJNABCDEFGHOPQUJUHDCKZUGLDCRUJUFUKUGADBCSTUAUBUCUD $.
      $( [16-Oct-1996] $)

    $( Theorem for alternate representation of ordered pairs, requiring
       Regularity.  Exercise 34 of [Enderton] p. 207. $)
    opthreg $p |- ( { A , { A , B } } = { C , { C , D } } <->
                   ( A = C /\ B = D ) ) $=
      ( cpr wceq wa wcel pri1 prex preleq mpanl12 preq1 eqeq1d preqr2 syl6bi
      imdistani syl adantr preq2 sylan9eq eqtrd impbi ) AABIZIZCCDIZIZJZACJZBDJ
      ZKZULUMUHUJJZKZUOAUHLCUJLULUQABEMCDGMAUHCUJEABNGCDNOPUMUPUNUMUPCBIZUJJUNU
      MUHURUJACBQZRBDCFHSTUAUBUOUICUHIZUKUMUIUTJUNACUHQUCUOUPUTUKJUMUNUHURUJUSB
      DCUDUEUHUJCUDUBUFUG $.
      $( [16-Oct-1996] $)
  $}

  $( The successor operation behaves like a one-to-one function (assuming the
     Axiom of Regularity).  Exercise 35 of [Enderton] p. 208 and its
     converse.  $)
  suc11reg $p |- ( suc A = suc B <-> A = B ) $=
    ( csuc wceq cvv wcel wi wa wn wo en2lp ianor mpbi eleq2 sucidg syl5bi
    com12 elsucg sylibd imp ord ex com23 syl5bir eqcom syl6ib jaao mpi nelneq
    sucexb negbii syl2anb pm2.21d ancoms syl5ib sucprc eqeqan12d biimpd 4cases
    suceq impbi ) ACZBCZDZABDZAEFZBEFZVDVEGZVFVGHABFZIZBAFZIZJZVHVIVKHIVMABKVIV
    KLMVFVJVHVGVLVFVDVJVEVFVDVJVEGVFVDHVIVEVFVDVIVEJZVFVDAVCFZVNVDVFVOVDAVBFVOV
    FVBVCANAEOPQABERSTUAUBUCVGVDVLVEVGVDVLVEGVGVDHZVLBADZVEVPVKVQVGVDVKVQJZVGVD
    BVBFZVRVDVGVSVDVSBVCFVGVBVCBNBEOUDQBAERSTUABAUEUFUBUCUGUHVFVGIZHVDVEVBEFZVC
    EFZIVDIVFVTVBVCEUIAUJZVGWBBUJZUKULUMVFIZVGHZVCVBDZVEVDWFWGVEVGWEWGIZWBWAIWH
    VGWEVCVBEUIWDVFWAWCUKULUNUMVBVCUEUOWEVTHVDVEWEVTVBAVCBAUPBUPUQURUSABUTVA $.
    $( [27-Oct-2003] $) $( [25-Oct-2003] $)

  ${
    $d f g x y A $.  $d f g x y B $.  $d f g v w x y z $.
    bren4reg.1 $e |- B e. V $.
    $( An equivalence to equinumerosity based on the ordered pair definition
       implied by ~ opthreg . $)
    bren4regOLD $p |- ( A ~~ B <-> E. f ( A. x e. A E! y e. B
    { x , { x , y } } e. f /\ A. x e. B E! y e. A { y , { y , x } } e. f ) ) $=
      ( vg vv vz vw cen wbr cv wreu wral wa wex cpr wcel bren4 wceq cop cab
      cdm cvv visset dmexg ax-mp opeldm adantl 19.23aiv crn rnexg opelrn csn
      snex pm3.26 ss2abi df-sn sseqtr4 ssexi abexex eleq2 reubidv ralbidv
      anbi12d cla4ev prex eqeq1 anbi1d 2exbidv elab opth opthreg bitr4 df-br
      anbi12i 2exbii wb breq12 copsex2g mp2an 3bitr2 reubii ralbii syl2anbr
      copab df-opab cuni uniex pri1 elunii mpan zfpair pri2 syl eqeltrr eqeltr
      breq preq1 preq2 eqtrd eleq1d eqid brab impbi bitr ) CDKLAMZBMZGMZLZBDNZA
      COZXIXHXJLZBCNZADOZPZGQZXHXHXIRZRZEMZSZBDNZACOZXIXIXHRZRZYASZBCNZADOZPZEQ
      ZABCDGFTXRYKXQYKGXTHMZIMZYMJMZRZRZUAZYMYNUBZXJSZPZJQZIQZHUCZSZBDNZACOZYFU
      UCSZBCNZADOZYKXMXPYJUUFUUIPEUUCUUAIHXJUDZXJUESZUUJUESGUFZXJUEUGUHYTYMUUJS
      ZJYSUUMYQYMYNXJIUFZUIUJUKYTJHXJULZUUKUUOUESUULXJUEUMUHYSYNUUOSYQYMYNXJUUN
      JUFZUNUJYTHUCZYPUOZYPUPUUQYQHUCUURYTYQHYQYSUQURHYPUSUTVAVBVBYAUUCUAZYDUUF
      YIUUIUUSYCUUEACUUSYBUUDBDYAUUCXTVCVDVEUUSYHUUHADUUSYGUUGBCYAUUCYFVCVDVEVF
      VGUUEXLACUUDXKBDUUDXTYPUAZYSPZJQIQZXHXIUBYRUAZYMYNXJLZPZJQIQZXKUUBUVBHXTX
      HXSVHYLXTUAZYTUVAIJUVGYQUUTYSYLXTYPVIVJVKVLUVEUVAIJUVCUUTUVDYSUVCXHYMUAXI
      YNUAPUUTXHXIYMYNAUFZBUFZUUPVMXHXIYMYNUVHUVIUUNUUPVNVOYMYNXJVPZVQVRXHUESZX
      IUESZUVFXKVSUVHUVIUVDXKIJXHXIUEUEYMXHYNXIXJVTWAWBWCWDWEUUHXOADUUGXNBCUUGY
      FYPUAZYSPZJQIQZXIXHUBYRUAZUVDPZJQIQZXNUUBUVOHYFXIYEVHYLYFUAZYTUVNIJUVSYQU
      VMYSYLYFYPVIVJVKVLUVQUVNIJUVPUVMUVDYSUVPXIYMUAXHYNUAPUVMXIXHYMYNUVIUVHUUP
      VMXIXHYMYNUVIUVHUUNUUPVNVOUVJVQVRUVLUVKUVRXNVSUVIUVHUVDXNIJXIXHUEUEYMXIYN
      XHXJVTWAWBWCWDWEWFUKYJXREXHXIYPYASZIJWGZLZBDNZACOZXIXHUWALZBCNZADOZXRYDYI
      XQUWDUWGPGUWAUWAYLYRUAZUVTPZJQZIQHUCUEUVTIJHWHUWJIHYAWIZYAEUFWJZUWIYMUWKS
      ZJUVTUWMUWHYMYPSUVTUWMYMYOUUNWKYMYPYAWLWMUJUKUWIJHUWKWIZUWKUWLWJUVTYNUWNS
      ZUWHUVTYOUWKSZUWOYOYPSUVTUWPYMYOIJWNWOYOYPYAWLWMYNYOSUWPUWOYMYNUUPWOYNYOU
      WKWLWMWPUJUWIHUCUWHHUCZYRUOUWQUEHYRUSYRUPWQUWIUWHHUWHUVTUQURVAVBVBWRXJUWA
      UAZXMUWDXPUWGUWRXLUWCACUWRXKUWBBDXHXIXJUWAWSVDVEUWRXOUWFADUWRXNUWEBCXIXHX
      JUWAWSVDVEVFVGUWCYCACUWBYBBDUVTXHXHYNRZRZYASYBIJXHXIUWAUVHUVIYMXHUAZYPUWT
      YAUXAYPXHYORZUWTYMXHYOWTUXAYOUWSUAUXBUWTUAYMXHYNWTYOUWSXHXAWPXBXCYNXIUAZU
      WTXTYAUXCUWSXSUAUWTXTUAYNXIXHXAUWSXSXHXAWPXCUWAXDZXEWDWEUWFYHADUWEYGBCUVT
      XIXIYNRZRZYASYGIJXIXHUWAUVIUVHYMXIUAZYPUXFYAUXGYPXIYORZUXFYMXIYOWTUXGYOUX
      EUAUXHUXFUAYMXIYNWTYOUXEXIXAWPXBXCYNXHUAZUXFYFYAUXIUXEYEUAUXFYFUAYNXHXIXA
      UXEYEXIXAWPXCUXDXEWDWEWFUKXFXG $.
      $( [12-Mar-2007] $) $( [4-Mar-2007] $)
  $}

  ${
    $d f g x y A $.  $d f g x y B $.  $d f g v w x y z $.
    $( An equivalence to the existence of an onto mapping, based on the ordered
       pair definition implied by ~ opthreg . $)
    exforegOLD $p |- ( E. f f : A -onto-> B <-> E. f ( A. x e. A E! y e. B
    { x , { x , y } } e. f /\ A. x e. B E. y e. A { y , { y , x } } e. f ) ) $=
      ( vg vv vz vw cv wfo wex wbr wreu wral wrex wa cpr wcel foeq1 cbvexv
      exfo bitr wceq cop cab cdm cvv visset dmexg ax-mp opeldm adantl 19.23aiv
      crn rnexg opelrn csn snex pm3.26 ss2abi df-sn sseqtr4 ssexi abexex eleq2
      reubidv ralbidv rexbidv anbi12d cla4ev prex eqeq1 anbi1d 2exbidv elab
      opth opthreg bitr4 df-br anbi12i 2exbii wb breq12 copsex2g mp2an 3bitr2
      reubii ralbii rexbii syl2anbr copab df-opab cuni uniex pri1 elunii mpan
      zfpair pri2 syl eqeltrr eqeltr breq preq1 preq2 eqtrd eleq1d eqid brab
      impbi ) CDEJZKZELZAJZBJZFJZMZBDNZACOZXPXOXQMZBCPZADOZQZFLZXOXOXPRZRZXLSZB
      DNZACOZXPXPXORZRZXLSZBCPZADOZQZELZXNCDXQKZFLYEXMYREFCDXLXQTUAABCDFUBUCYEY
      QYDYQFYGGJZHJZYTIJZRZRZUDZYTUUAUEZXQSZQZILZHLZGUFZSZBDNZACOZYLUUJSZBCPZAD
      OZYQXTYCYPUUMUUPQEUUJUUHHGXQUGZXQUHSZUUQUHSFUIZXQUHUJUKUUGYTUUQSZIUUFUUTU
      UDYTUUAXQHUIZULUMUNUUGIGXQUOZUURUVBUHSUUSXQUHUPUKUUFUUAUVBSUUDYTUUAXQUVAI
      UIZUQUMUUGGUFZUUCURZUUCUSUVDUUDGUFUVEUUGUUDGUUDUUFUTVAGUUCVBVCVDVEVEXLUUJ
      UDZYJUUMYOUUPUVFYIUULACUVFYHUUKBDXLUUJYGVFVGVHUVFYNUUOADUVFYMUUNBCXLUUJYL
      VFVIVHVJVKUULXSACUUKXRBDUUKYGUUCUDZUUFQZILHLZXOXPUEUUEUDZYTUUAXQMZQZILHLZ
      XRUUIUVIGYGXOYFVLYSYGUDZUUGUVHHIUVNUUDUVGUUFYSYGUUCVMVNVOVPUVLUVHHIUVJUVG
      UVKUUFUVJXOYTUDXPUUAUDQUVGXOXPYTUUAAUIZBUIZUVCVQXOXPYTUUAUVOUVPUVAUVCVRVS
      YTUUAXQVTZWAWBXOUHSZXPUHSZUVMXRWCUVOUVPUVKXRHIXOXPUHUHYTXOUUAXPXQWDWEWFWG
      WHWIUUOYBADUUNYABCUUNYLUUCUDZUUFQZILHLZXPXOUEUUEUDZUVKQZILHLZYAUUIUWBGYLX
      PYKVLYSYLUDZUUGUWAHIUWFUUDUVTUUFYSYLUUCVMVNVOVPUWDUWAHIUWCUVTUVKUUFUWCXPY
      TUDXOUUAUDQUVTXPXOYTUUAUVPUVOUVCVQXPXOYTUUAUVPUVOUVAUVCVRVSUVQWAWBUVSUVRU
      WEYAWCUVPUVOUVKYAHIXPXOUHUHYTXPUUAXOXQWDWEWFWGWJWIWKUNYPYEEXOXPUUCXLSZHIW
      LZMZBDNZACOZXPXOUWHMZBCPZADOZYEYJYOYDUWKUWNQFUWHUWHYSUUEUDZUWGQZILZHLGUFU
      HUWGHIGWMUWQHGXLWNZXLEUIWOZUWPYTUWRSZIUWGUWTUWOYTUUCSUWGUWTYTUUBUVAWPYTUU
      CXLWQWRUMUNUWPIGUWRWNZUWRUWSWOUWGUUAUXASZUWOUWGUUBUWRSZUXBUUBUUCSUWGUXCYT
      UUBHIWSWTUUBUUCXLWQWRUUAUUBSUXCUXBYTUUAUVCWTUUAUUBUWRWQWRXAUMUWPGUFUWOGUF
      ZUUEURUXDUHGUUEVBUUEUSXBUWPUWOGUWOUWGUTVAVDVEVEXCXQUWHUDZXTUWKYCUWNUXEXSU
      WJACUXEXRUWIBDXOXPXQUWHXDVGVHUXEYBUWMADUXEYAUWLBCXPXOXQUWHXDVIVHVJVKUWJYI
      ACUWIYHBDUWGXOXOUUARZRZXLSYHHIXOXPUWHUVOUVPYTXOUDZUUCUXGXLUXHUUCXOUUBRZUX
      GYTXOUUBXEUXHUUBUXFUDUXIUXGUDYTXOUUAXEUUBUXFXOXFXAXGXHUUAXPUDZUXGYGXLUXJU
      XFYFUDUXGYGUDUUAXPXOXFUXFYFXOXFXAXHUWHXIZXJWHWIUWMYNADUWLYMBCUWGXPXPUUARZ
      RZXLSYMHIXPXOUWHUVPUVOYTXPUDZUUCUXMXLUXNUUCXPUUBRZUXMYTXPUUBXEUXNUUBUXLUD
      UXOUXMUDYTXPUUAXEUUBUXLXPXFXAXGXHUUAXOUDZUXMYLXLUXPUXLYKUDUXMYLUDUUAXOXPX
      FUXLYKXPXFXAXHUXKXJWJWIWKUNXKUC $.
      $( [22-Mar-2007] $) $( [20-Mar-2007] $)
  $}

