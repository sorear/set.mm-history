$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Introduce the Axiom of Regularity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Axiom of Regularity.  An axiom of Zermelo-Fraenkel set theory.  Also
       called the Axiom of Foundation.  A rather non-intuitive axiom that
       denies more than it asserts, it states (in the form of ~ zfreg ) that
       every non-empty set contains a set disjoint from itself.  One
       consequence is that it denies the existence of a set containing itself
       ( ~ elirrv ).  A stronger version that works for proper classes is
       proved as ~ zfregs . $)
    ax-reg $a |- ( E. y y e. x ->
                 E. y ( y e. x /\ A. z ( z e. y -> -. z e. x ) ) ) $.

    $( Axiom of Regularity expressed more compactly. $)
    axreg $p |- ( x e. y ->
                 E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) $=
      ( cv wcel wn wi wal wa wex ax-reg 19.23bi ) ADZBDZEZOCDZMEPNEFGCHIAJABACK
      L $.
      $( [14-Aug-2003] $)
  $}

  ${
    $d x y A z $.
    zfregcl.1 $e |- A e. _V $.
    $( The Axiom of Regularity with class variables. $)
    zfregcl $p |- ( E. x x e. A -> E. x e. A A. y e. x -. y e. A ) $=
      ( vz cv wcel wex wn wral wrex wceq eleq2 exbidv notbid ralbidv rexeqbi1dv
      wi imbi12d hbre1 wa axreg df-ral rexbii df-rex bitr2i sylib 19.23ai vtocl
      wal ) AFZEFZGZAHZBFZULGZIZBUKJZAULKZRUKCGZAHZUOCGZIZBUKJZACKZRECDULCLZUNV
      AUSVEVFUMUTAULCUKMNURVDAULCVFUQVCBUKVFUPVBULCUOMOPQSUMUSAURAULTUMUMUOUKGU
      QRBUJZUAAHZUSAEBUBUSVGAULKVHURVGAULUQBUKUCUDVGAULUEUFUGUHUI $.
      $( [7-May-2004] $) $( [5-Aug-1994] $)
  $}

  ${
    $d x A y $.
    zfreg.1 $e |- A e. _V $.
    $( The Axiom of Regularity using abbreviations.  Axiom 6 of [TakeutiZaring]
       p. 21.  This is called the "weak form."  There is also a "strong form,"
       not requiring that ` A ` be a set, that can be proved with more
       difficulty (see ~ zfregs ). $)
    zfreg $p |- ( A =/= (/) -> E. x e. A ( x i^i A ) = (/) ) $=
      ( vy cv wcel wex wn wral wrex c0 wne cin wceq zfregcl disj rexbii 3imtr4i
      n0 ) AEZBFAGDEBFHDTIZABJBKLTBMKNZABJADBCOABSUBUAABDTBPQR $.
      $( [11-Apr-2007] $) $( [26-Nov-1995] $)
  $}

  ${
    $d x A y $.
    zfreg2.1 $e |- A e. _V $.
    $( The Axiom of Regularity using abbreviations.  This form with the
       intersection arguments commuted (compared to ~ zfreg ) is formally more
       convenient for us in some cases.  Axiom Reg of [BellMachover] p. 480. $)
    zfreg2 $p |- ( A =/= (/) -> E. x e. A ( A i^i x ) = (/) ) $=
      ( vy cv wcel wex wn wral wrex c0 wne wceq zfregcl n0 disjr rexbii 3imtr4i
      cin ) AEZBFAGDEBFHDTIZABJBKLBTSKMZABJADBCNABOUBUAABDBTPQR $.
      $( [12-Apr-2007] $) $( [17-Sep-2003] $)
  $}

  ${
    $d x y z $.
    $( The membership relation is irreflexive: no set is a member of itself.
       Theorem 105 of [Suppes] p. 54.  (This is trivial to prove from ~ zfregfr
       and ~ efrirr , but this proof is direct from the Axiom of
       Regularity.) $)
    elirrv $p |- -. x e. x $=
      ( vz vy cv wcel csn wn wral wrex wex eleq1 visset snid a4eiv snex zfregcl
      ax-mp wceq elsn wi ax-14 equcoms com12 syl5bi notbid mt2i nsyli r19.21aiv
      rcla4cv con2d ralnex sylib mt2 ) ADZUNEZBDZUNFZEZGZBCDZHZCUQIZUTUQEZCJVBV
      CUNUQEZCAUTUNUQKUNALMZNCBUQUNOPQUOVAGZCUQHVBGUOVFCUQUOVAVCUOVCUNUTEZVAVCU
      TUNRZUOVGCUNSVHUOVGUOVGTACACAUAUBUCUDVAVGVDVEUSVDGBUNUTUPUNRURVDUPUNUQKUE
      UIUFUGUJUHVACUQUKULUM $.
      $( [19-Aug-1993] $)
  $}

  ${
    $d A x $.
    $( No class is a member of itself.  Exercise 6 of [TakeutiZaring] p. 22.
       (The proof was shortened by Andrew Salmon, 9-Jul-2011.) $)
    elirr $p |- -. A e. A $=
      ( vx wcel wn wi cv wceq id eleq12d notbid elirrv vtoclg pm2.01 ax-mp ) AA
      CZODZEPBFZQCZDPBAAQAGZROSQAQASHZTIJBKLOMN $.
      $( [9-Jul-2011] $) $( [7-Aug-1994] $)

    $( A class is equal to its successor iff it is a proper class (assuming the
       Axiom of Regularity). $)
    sucprcreg $p |- ( -. A e. _V <-> suc A = A ) $=
      ( vx cvv wcel wn csuc wceq sucprc cv wal elirr ax-17 eleq1 ceqsalg mtbiri
      wi csn elsn wss syl5bir wo olc cun elun ssid wb df-suc eqeq1i sseq1 sylbi
      mpbiri sseld syl5 19.21aiv nsyl3 impbii ) ACDZEAFZAGZAHUQBIZAGZUTADZPZBJZ
      USUQVDAADZAKVBVEBACVEBLUTAAMNOUSVCBVAUTAQZDZUSVBBARVGVBVGUAZUSVBVGVBUBVHU
      TAVFUCZDUSVBUTAVFUDUSVIAUTUSVIASZAASZAUEUSVIAGVJVKUFURVIAAUGUHVIAAUIUJUKU
      LTUMTUNUOUP $.
      $( [6-Jan-2004] $) $( [9-Jul-2004] $)
  $}

  $( The Russell class is equal to the universe ` _V ` .  Exercise 5 of
     [TakeutiZaring] p. 22.  (Contributed by Alan Sare, 4-Oct-2008.) $)
  ruv $p |- { x | x e/ x } = _V $=
    ( cvv cv wceq cab wnel df-v equid wcel elirrv df-nel mpbir 2th abbii eqtr2i
    wn ) BACZQDZAEQQFZAEAGRSARSAHSQQIPAJQQKLMNO $.
    $( [4-Oct-2008] $)

  $( Alternate proof of Russell's Paradox ~ ru , simplified using (indirectly)
     the Axiom of Regularity ~ ax-reg .  (Contributed by Alan Sare,
     4-Oct-2008.) $)
  ruALT $p |- { x | x e/ x } e/ _V $=
    ( cv wnel cab cvv wcel wn vprc df-nel mpbir wceq wb ruv neleq1 ax-mp ) ABZP
    CADZECZEECZSEEFGHEEIJQEKRSLAMQEENOJ $.
    $( [4-Oct-2008] $)

  ${
    $d x y A $.
    $( The epsilon relation is founded on any class. $)
    zfregfr $p |- _E Fr A $=
      ( vx vy cep wfr cv wss c0 wne wa wceq wrex wi dfepfr visset zfreg2 adantl
      cin mpgbir ) ADEBFZAGZTHIZJTCFRHKCTLZMBBCANUBUCUACTBOPQS $.
      $( [26-Nov-1995] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( No class has 2-cycle membership loops.  Theorem 7X(b) of [Enderton]
       p. 206. $)
    en2lp $p |- -. ( A e. B /\ B e. A ) $=
      ( vx vy cvv wcel wa wn cv wceq eleq1 eleq2 anbi12d notbid cep wfr zfregfr
      visset pm3.2i elisset efrn2lp mp2an vtocl2g anim12i con3i pm2.61i ) AEFZB
      EFZGZABFZBAFZGZHZCIZDIZFZUOUNFZGZHZAUOFZUOAFZGZHUMCDABEEUNAJZURVBVCUPUTUQ
      VAUNAUOKUNAUOLMNUOBJZVBULVDUTUJVAUKUOBALUOBAKMNEOPUNEFZUOEFZGUSEQVEVFCRDR
      SCDEUAUBUCULUIUJUGUKUHABTBATUDUEUF $.
      $( [16-Oct-1996] $)

  $}

  ${
    preleq.1 $e |- A e. _V $.
    preleq.2 $e |- B e. _V $.
    preleq.3 $e |- C e. _V $.
    preleq.4 $e |- D e. _V $.
    $( Equality of two unordered pairs when one member of each pair contains
       the other member. $)
    preleq $p |- ( ( ( A e. B /\ C e. D ) /\ { A , B } = { C , D } ) ->
                   ( A = C /\ B = D ) ) $=
      ( cpr wceq wcel wa wn wo preq12b biimpi ord en2lp eleq12 anbi1d mtbiri
      syl6 con4d impcom ) ABICDIJZABKZCDKZLZACJBDJLZUEUIUHUEUIMADJBCJLZUHMUEUIU
      JUEUIUJNABCDEFGHOPQUJUHDCKZUGLDCRUJUFUKUGADBCSTUAUBUCUD $.
      $( [16-Oct-1996] $)

    $( Theorem for alternate representation of ordered pairs, requiring the
       Axiom of Regularity ~ ax-reg (via the ~ preleq step).  See ~ df-op for a
       description of other ordered pair representations.  Exercise 34 of
       [Enderton] p. 207. $)
    opthreg $p |- ( { A , { A , B } } = { C , { C , D } } <->
                   ( A = C /\ B = D ) ) $=
      ( cpr wceq wa wcel prid1 prex preleq mpanl12 preq1 eqeq1d preqr2 syl6bi
      imdistani syl adantr preq12 preq2d eqtrd impbii ) AABIZIZCCDIZIZJZACJZBDJ
      ZKZULUMUHUJJZKZUOAUHLCUJLULUQABEMCDGMAUHCUJEABNGCDNOPUMUPUNUMUPCBIZUJJUNU
      MUHURUJACBQRBDCFHSTUAUBUOUICUHIZUKUMUIUSJUNACUHQUCUOUHUJCABCDUDUEUFUG $.
      $( [16-Oct-1996] $)
  $}

  $( The successor operation behaves like a one-to-one function (assuming the
     Axiom of Regularity).  Exercise 35 of [Enderton] p. 208 and its
     converse. $)
  suc11reg $p |- ( suc A = suc B <-> A = B ) $=
    ( csuc wceq cvv wcel wi wa wn wo sucidg eleq2 elsucg sylibd imp com23 eqcom
    ord ex sucexb en2lp mpbi syl5ibcom syl5ibrcom syl6ib jaao mpi notbii nelneq
    ianor syl2anb pm2.21d ancoms syl5bi sucprc eqeqan12d biimpd 4cases impbii
    suceq ) ACZBCZDZABDZAEFZBEFZVCVDGZVEVFHABFZIZBAFZIZJZVGVHVJHIVLABUAVHVJUJUB
    VEVIVGVFVKVEVCVIVDVEVCVIVDGVEVCHVHVDVEVCVHVDJZVEVCAVBFZVMVEAVAFVCVNAEKVAVBA
    LUCABEMNORSPVFVCVKVDVFVCVKVDGVFVCHZVKBADZVDVOVJVPVFVCVJVPJZVFVCBVAFZVQVFVRV
    CBVBFBEKVAVBBLUDBAEMNORBAQUESPUFUGVEVFIZHVCVDVEVAEFZVBEFZIVCIVSATZVFWABTZUH
    VAVBEUIUKULVCVBVADZVEIZVFHZVDVAVBQWFWDVDVFWEWDIZVFWAVTIWGWEWCVEVTWBUHVBVAEU
    IUKUMULUNWEVSHVCVDWEVSVAAVBBAUOBUOUPUQURABUTUS $.
    $( [25-Oct-2003] $)

  ${
    $d x y A $.
    $( Assuming ~ ax-reg , an ordinal is a transitive class on which inclusion
       satisfies trichotomy.  (Contributed by Scott Fenton, 27-Oct-2010.) $)
    dford2 $p |- ( Ord A <-> ( Tr A /\
                    A. x e. A A. y e. A ( x e. y \/ x = y \/ y e. x ) ) ) $=
      ( word wtr cep wwe wa cv wcel wceq w3o wral df-ord wfr wbr dfwe2 biantrur
      zfregfr epel biid 3orbi123i 2ralbii 3bitr2i anbi2i bitri ) CDCEZCFGZHUGAI
      ZBIZJZUIUJKZUJUIJZLZBCMACMZHCNUHUOUGUHCFOZUIUJFPZULUJUIFPZLZBCMACMZHUTUOA
      BCFQUPUTCSRUSUNABCCUQUKULULURUMABTULUABATUBUCUDUEUF $.
      $( [27-Oct-2010] $)
  $}


