$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The axioms of "pure" predicate calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbols needed for pure predicate calculus. $)
  $c A. $. $( "inverted A" universal quantifier (read:  "for all") $)
  $c set $. $( Individual variable type (read:  "the following is an
             individual (set) variable" $)

  $( Declare some names for individual variables. $)
  $v x $.
  $v y $.
  $v z $.
  $v w $.
  $v v $.
  $v u $.
  $( Let ` x ` be an individual variable. $)
  vx $f set x $.
  $( Let ` y ` be an individual variable. $)
  vy $f set y $.
  $( Let ` z ` be an individual variable. $)
  vz $f set z $.
  $( Let ` w ` be an individual variable. $)
  vw $f set w $.
  $( Let ` v ` be an individual variable. $)
  vv $f set v $.
  $( Let ` u ` be an individual variable. $)
  vu $f set u $.

  $( Extend wff definition to include the universal quantifier ('for all').
     ` A. x ph ` is read " ` ph ` (phi) is true for all ` x ` ."  Typically,
     in its final application ` ph ` would be replaced with a wff containing
     a (free) occurrence of the variable ` x ` , for example ` x = y ` .
     In a universe with a finite number of objects, "for all" is equivalent
     to a big conjunction (AND) with one wff for each possible case of
     ` x ` .  When the universe is infinite (as with set theory), such a
     propositional-calculus equivalent is not possible because an infinitely
     long formula has no meaning, but conceptually the idea is the same. $)
  wal $a wff A. x ph $.

  $( Axiom of Specialization.  A quantified wff implies the wff without a
     quantifier (i.e. an instance, or special case, of the generalized wff).
     In other words if something is true for all ` x ` , it is true for any
     specific ` x ` (that would typically occur as a free variable in the wff
     substituted for ` ph ` ). (A free variable is one that does not occur in
     the scope of a quantifier:  ` x ` and ` y ` are both free in
     ` x = y ` , but only ` y ` is free in ` A. x x = y ` .) This is one of
     the 4 axioms of what we call "pure" predicate calculus.  Unlike the more
     typical textbook Axiom of Specialization, we cannot choose a variable
     different from ` x ` for the special case.  That is dealt with later
     when substitution is introduced - see ~ stdpc4 .  Axiom scheme C5' in
     [Megill] p. 448 (p. 16 of the preprint).  Also appears as Axiom B5 of
     [Tarski] p. 67 (under his system S2, defined in the last paragraph on
     p. 77).  Note that the converse of this axiom does not hold in general,
     but a weaker inference form of the converse holds and is expressed as
     rule ~ ax-gen .  Conditional forms of the converse are given by ~ ax-12 ,
     ~ ax-15 , ~ ax-16 , and ~ ax-17 .

     _General remarks_:  Predicate calculus, or first-order logic, introduces
     quantifiers to make statements such as "for all individuals,
     such-and-such is true" and "there exist individuals such that...  ."  We
     introduce a new kind of variable, called an "individual variable," that
     ranges over individuals.  (Actually, in Metamath we are introducing
     "metavariables" that range over the individual variables of textbook
     predicate calculus, but the theorems look the same.  This is a technical
     point you should be aware of when studying standard textbooks.)  In
     addition, predicate calculus introduces one or more "predicate symbols"
     that combine individual variables to form wff"s.  We will be concerned
     with two predicate symbols, the equality sign ` = ` used in all of
     mathematics and the stylized epsilon ` e. ` used to express "is an
     element of" in set theory.

     Our axioms look quite different from those in standard textbooks, but the
     rules for manipulating the symbols end up being considerably simpler.
     The axioms of standard textbooks are derived as theorems ~ stdpc4 and
     ~ stdpc5 .

     We will work with the axioms for predicate calculus in four phases.
     Phase 1 introduces "pure" predicate calculus, which has no predicate
     symbols.  Phase 2, starting at ~ ax-8 , introduces the predicate symbol
     for equality.  Phase 3, starting at ~ ax-13 , introduces the stylized
     epsilon predicate symbol for set theory (without specifying any of its
     properties that are peculiar to set theory).  Phase 4, starting at
     ~ ax-17 , introduces the concept of distinct variables (our first use of
     the $d statement).

     After phase 3, we will define ( ~ df-sb ) and develop the concept of
     proper substitution.  In standard textbooks, substitution is introduced
     immediately with a somewhat complex recursive definition, since it is
     needed to state the axioms.  Instead, we will define it in terms of
     concepts contained in the axioms so that in principle it can be
     eliminated from the language entirely.

     Finally, we will define existential uniqueness ( ~ df-eu ) and develop
     some basic facts about it.

     ~ ax-4 through ~ ax-7 are the axioms of what we call "pure" predicate
     calculus.  These are valid even when their quantified variables ` x `
     and ` y ` occur ("free" or not) in wff's ` ph ` and ` ps ` . Thus we do
     not have to worry about "free" variable restrictions that complicate the
     traditional textbook axioms.

     An alternate axiomatization could use ~ ax467 in place of ~ ax-4 ,
     ~ ax-6 , and ~ ax-7 . $)
  ax-4 $a |- ( A. x ph -> ph ) $.

  $( Axiom of Quantified Implication.  This axiom moves a quantifier from
     outside to inside an implication, quantifying ` ps ` .  Notice that
     ` x ` must not be a free variable in the antecedent of the quantified
     implication, and we express this by binding ` ph ` to "protect" the
     axiom from a ` ph ` containing a free ` x ` .
     One of the 4 axioms of pure predicate calculus.  Axiom scheme C4' in
     [Megill] p. 448 (p. 16 of the preprint).  It is a special case of Lemma 5
     of [Monk2] p. 108 and Axiom 5 of [Mendelson] p. 69. $)
  ax-5 $a |- ( A. x ( A. x ph -> ps ) -> ( A. x ph -> A. x ps ) ) $.

  $( Axiom of Quantified Negation.  This axiom is used to manipulate negated
     quantifiers.  One of the 4 axioms of pure predicate calculus.  Equivalent
     to axiom scheme C7' in [Megill] p. 448 (p. 16 of the preprint).  Another
     equivalent variant ~ ax6-2 appears as Axiom C5-2 of [Monk2] p. 113.  An
     alternate axiomatization could use ~ ax467 in place of ~ ax-4 , ~ ax-6 ,
     and ~ ax-7 . $)
  ax-6 $a |- ( -. A. x -. A. x ph -> ph ) $.

  $( Axiom of Quantifier Commutation.  This axiom says universal quantifiers
     can be swapped.  One of the 4 axioms of pure predicate calculus.  Axiom
     scheme C6' in [Megill] p. 448 (p. 16 of the preprint).  Also appears as
     Lemma 12 of [Monk2] p. 109.  An alternate axiomatization could use
     ~ ax467 in place of ~ ax-4 , ~ ax-6 , and ~ ax-7 . $)
  ax-7 $a |- ( A. x A. y ph -> A. y A. x ph ) $.

  ${
    ax-g.1 $e |- ph $.
    $( Rule of Generalization.  The postulated inference rule of pure predicate
       calculus.  See e.g. Rule 2 of [Hamilton] p. 74.  This rule says that if
       something is unconditionally true, then it is true for all values of a
       variable.  For example, if we have proved ` x = x ` , we can conclude
       ` A. x x = x ` or even ` A. y x = x ` .  Theorem ~ a4i shows we can go
      the other way also:  in other words we can add or remove universal
      quantifiers from the beginning of any theorem as required. $)
    ax-gen $a |- A. x ph $.
  $}


  $( Declare the existential quantifier symbol. $)
  $c E. $.   $( Backwards E (read:  "there exists") $)

  $( Extend wff definition to include the existential quantifier ("there
     exists"). $)
  wex $a wff E. x ph $.

  $( Define existential quantification.  ` E. x ph ` means "there exists at
     least one set ` x ` such that ` ph ` is true."  Definition of [Margaris]
     p. 49. $)
  df-ex $a |- ( E. x ph <-> -. A. x -. ph ) $.

  ${
    a4i.1 $e |- A. x ph $.
    $( Inference rule reversing generalization. $)
    a4i $p |- ph $=
      ( wal ax-4 ax-mp ) ABDACABEF $.
      $( [5-Aug-1993] $)
  $}

  ${
    gen2.1 $e |- ph $.
    $( Generalization applied twice. $)
    gen2 $p |- A. x A. y ph $=
      ( wal ax-gen ) ACEBACDFF $.
      $( [30-Apr-1998] $)
  $}

  ${
    a4s.1 $e |- ( ph -> ps ) $.
    $( Generalization of antecedent. $)
    a4s $p |- ( A. x ph -> ps ) $=
      ( wal ax-4 syl ) ACEABACFDG $.
      $( [5-Aug-1993] $)
  $}

  ${
    a4sd.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction generalizing antecedent. $)
    a4sd $p |- ( ph -> ( A. x ps -> ch ) ) $=
      ( wal ax-4 syl5 ) ABCBDFEBDGH $.
      $( [17-Aug-1994] $)
  $}

  ${
    mpg.1 $e |- ( A. x ph -> ps ) $.
    mpg.2 $e |- ph $.
    $( Modus ponens combined with generalization. $)
    mpg $p |- ps $=
      ( wal ax-gen ax-mp ) ACFBACEGDH $.
      $( [24-May-1994] $)
  $}

  ${
    mpgbi.1 $e |- ( A. x ph <-> ps ) $.
    mpgbi.2 $e |- ph $.
    $( Modus ponens on biconditional combined with generalization. $)
    mpgbi $p |- ps $=
      ( wal biimp mpg ) ABCACFBDGEH $.
      $( [24-May-1994] $)
  $}

  ${
    mpgbir.1 $e |- ( ph <-> A. x ps ) $.
    mpgbir.2 $e |- ps $.
    $( Modus ponens on biconditional combined with generalization. $)
    mpgbir $p |- ph $=
      ( wal biimpr mpg ) BACABCFDGEH $.
      $( [24-May-1994] $)
  $}

  ${
    a5i.1 $e |- ( A. x ph -> ps ) $.
    $( Inference from ~ ax-5 . $)
    a5i $p |- ( A. x ph -> A. x ps ) $=
      ( wal wi ax-5 mpg ) ACEZBFIBCEFCABCGDH $.
      $( [5-Aug-1993] $)
  $}

  $( Abbreviated version of ~ ax-6 . $)
  a6e $p |- ( E. x A. x ph -> ph ) $=
    ( wal wex wn df-ex ax-6 sylbi ) ABCZBDIEBCEAIBFABGH $.
    $( [5-Aug-1993] $)

  ${
    a7s.1 $e |- ( A. x A. y ph -> ps ) $.
    $( Swap quantifiers in an antecedent. $)
    a7s $p |- ( A. y A. x ph -> ps ) $=
      ( wal ax-7 syl ) ACFDFADFCFBADCGEH $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.20i.1 $e |- ( ph -> ps ) $.
    $( Inference quantifying both antecedent and consequent. $)
    19.20i $p |- ( A. x ph -> A. x ps ) $=
      ( a4s a5i ) ABCABCDEF $.
      $( [5-Aug-1993] $)

    $( Inference doubly quantifying both antecedent and consequent. $)
    19.20i2 $p |- ( A. x A. y ph -> A. x A. y ps ) $=
      ( wal 19.20i ) ADFBDFCABDEGG $.
      $( [5-Feb-2005] $) $( [3-Feb-2005] $)
  $}

  $( Theorem 19.20 of [Margaris] p. 90.  (The proof was shortened by Mel L.
     O'Cat, 30-Mar-2008.) $)
  19.20 $p |- ( A. x ( ph -> ps ) -> ( A. x ph -> A. x ps ) ) $=
    ( wi wal id a4sd 19.20i ax-5 syl ) ABDZCEACEZBDZCELBCEDKMCKABCKFGHABCIJ $.
    $( [30-Mar-2008] $) $( [5-Aug-1993] $)

  $( Theorem 19.20 of [Margaris] p. 90.  (The proof was shortened by Mel L.
     O'Cat, 28-Feb-2008.) $)
  19.20OLD $p |- ( A. x ( ph -> ps ) -> ( A. x ph -> A. x ps ) ) $=
    ( wi wal ax-4 syl5 a5i ax-5 syl ) ABDZCEZACEZBDZCEMBCEDKNCLABMKCFACFGHABCIJ
    $.
    $( [28-Feb-2008] $) $( [5-Aug-1993] $)

  $( Theorem 19.20 of [Margaris] p. 90. $)
  19.20OLDOLD $p |- ( A. x ( ph -> ps ) -> ( A. x ph -> A. x ps ) ) $=
    ( wi wal ax-4 imim1i a4s a5i ax-5 syl ) ABDZCEACEZBDZCEMBCEDLNCLNCMABACFGHI
    ABCJK $.
    $( [5-Aug-1993] $)

  ${
    19.20ii.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Inference quantifying antecedent, nested antecedent, and consequent. $)
    19.20ii $p |- ( A. x ph -> ( A. x ps -> A. x ch ) ) $=
      ( wal wi 19.20i 19.20 syl ) ADFBCGZDFBDFCDFGAKDEHBCDIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.20d.1 $e |- ( ph -> A. x ph ) $.
    19.20d.2 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.20 of [Margaris] p. 90. $)
    19.20d $p |- ( ph -> ( A. x ps -> A. x ch ) ) $=
      ( wal wi 19.20ii syl ) AADGBDGCDGHEABCDFIJ $.
      $( [4-Jan-2002] $)
  $}

  $( Theorem 19.15 of [Margaris] p. 90. $)
  19.15 $p |- ( A. x ( ph <-> ps ) -> ( A. x ph <-> A. x ps ) ) $=
    ( wb wal bi1 19.20ii bi2 impbid ) ABDZCEACEBCEJABCABFGJBACABHGI $.
    $( [5-Aug-1993] $)

  ${
    19.21ai.1 $e |- ( ph -> A. x ph ) $.
    19.21ai.2 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90. $)
    19.21ai $p |- ( ph -> A. x ps ) $=
      ( wal 19.20i syl ) AACFBCFDABCEGH $.
      $( [5-Aug-1993] $)
  $}

  ${
    albii.1 $e |- ( ph <-> ps ) $.
    $( Inference adding universal quantifier to both sides of an
       equivalence. $)
    albii $p |- ( A. x ph <-> A. x ps ) $=
      ( wb wal 19.15 mpg ) ABEACFBCFECABCGDH $.
      $( [7-Aug-1994] $)

    $( Inference adding 2 universal quantifiers to both sides of an
       equivalence. $)
    2albii $p |- ( A. x A. y ph <-> A. x A. y ps ) $=
      ( wal albii ) ADFBDFCABDEGG $.
      $( [9-Mar-1997] $)
  $}

  ${
    hbth.1 $e |- ph $.
    $( No variable is (effectively) free in a theorem.

       This and later "hypothesis-building" lemmas, with labels starting
       "hb...", allow us to construct proofs of formulas of the form
       ` |- ( ph -> A. x ph ) ` from smaller formulas of this form.  These are
       useful for constructing hypotheses that state
       " ` x ` is (effectively) not free in ` ph ` ." $)
    hbth $p |- ( ph -> A. x ph ) $=
      ( wal ax-gen a1i ) ABDAABCEF $.
      $( [5-Aug-1993] $)
  $}

  $( Closed theorem version of bound-variable hypothesis builder ~ hbn . $)
  hbnt $p |- ( A. x ( ph -> A. x ph ) -> ( -. ph -> A. x -. ph ) ) $=
    ( wal wi wn con3 19.20ii ax-6 con1i syl5 ) AABCZDZBCKEZBCZAEZBCOLMOBAKFGNAA
    BHIJ $.
    $( [5-Aug-1993] $)

  $( ` x ` is not free in ` A. x ph ` .  Example in Appendix in [Megill]
     p. 450 (p. 19 of the preprint). $)
  hba1 $p |- ( A. x ph -> A. x A. x ph ) $=
    ( wal id a5i ) AABCZBFDE $.
    $( [5-Aug-1993] $)

  ${
    hb.1 $e |- ( ph -> A. x ph ) $.
    $( If ` x ` is not free in ` ph ` , it is not free in ` -. ph ` . $)
    hbn $p |- ( -. ph -> A. x -. ph ) $=
      ( wal wi wn hbnt mpg ) AABDEAFZIBDEBABGCH $.
      $( [5-Aug-1993] $)

    $( If ` x ` is not free in ` ph ` , it is not free in ` A. y ph ` . $)
    hbal $p |- ( A. y ph -> A. x A. y ph ) $=
      ( wal 19.20i ax-7 syl ) ACEZABEZCEIBEAJCDFACBGH $.
      $( [5-Aug-1993] $)

    $( If ` x ` is not free in ` ph ` , it is not free in ` E. y ph ` . $)
    hbex $p |- ( E. y ph -> A. x E. y ph ) $=
      ( wn wal wex hbn hbal df-ex albii 3imtr4 ) AEZCFZEZOBFACGZPBFNBMBCABDHIHA
      CJZPOBQKL $.
      $( [5-Aug-1993] $)
    ${

      hb.2 $e |- ( ps -> A. x ps ) $.
      $( If ` x ` is not free in ` ph ` and ` ps ` , it is not free in
         ` ( ph -> ps ) ` .  (The proof was shortened by Mel L. O'Cat,
         3-Mar-2008; see ~ hbimOLD for previous version.) $)
      hbim $p |- ( ( ph -> ps ) -> A. x ( ph -> ps ) ) $=
        ( wi wal wn hbn pm2.21 19.21ai ax-1 ja ) ABABFZCGAHNCACDIABJKBNCEBALKM
        $.
        $( [4-Mar-2008] $)  $( [5-Aug-1993] $)

      $( If ` x ` is not free in ` ph ` and ` ps ` , it is not free in
         ` ( ph \/ ps ) ` . $)
      hbor $p |- ( ( ph \/ ps ) -> A. x ( ph \/ ps ) ) $=
        ( wn wi wal wo hbn hbim df-or albii 3imtr4 ) AFZBGZPCHABIZQCHOBCACDJEKA
        BLZQPCRMN $.
        $( [5-Aug-1993] $)

      $( If ` x ` is not free in ` ph ` and ` ps ` , it is not free in
         ` ( ph /\ ps ) ` . $)
      hban $p |- ( ( ph /\ ps ) -> A. x ( ph /\ ps ) ) $=
        ( wn wi wal wa hbn hbim df-an albii 3imtr4 ) ABFZGZFZQCHABIZRCHPCAOCDBC
        EJKJABLZRQCSMN $.
        $( [5-Aug-1993] $)

      $( If ` x ` is not free in ` ph ` and ` ps ` , it is not free in
         ` ( ph <-> ps ) ` . $)
      hbbi $p |- ( ( ph <-> ps ) -> A. x ( ph <-> ps ) ) $=
        ( wi wa wal wb hbim hban bi albii 3imtr4 ) ABFZBAFZGZQCHABIZRCHOPCABCDE
        JBACEDJKABLZRQCSMN $.
        $( [5-Aug-1993] $)

      ${

        hb.3 $e |- ( ch -> A. x ch ) $.
        $( If ` x ` is not free in ` ph ` , ` ps ` , and ` ch ` , it is not
           free in ` ( ph \/ ps \/ ch ) ` . $)
        hb3or $p |- ( ( ph \/ ps \/ ch ) -> A. x ( ph \/ ps \/ ch ) ) $=
          ( wo wal w3o hbor df-3or albii 3imtr4 ) ABHZCHZPDIABCJZQDIOCDABDEFKGK
          ABCLZQPDRMN $.
          $( [14-Sep-2003] $)

        $( If ` x ` is not free in ` ph ` , ` ps ` , and ` ch ` , it is not
           free in ` ( ph /\ ps /\ ch ) ` . $)
        hb3an $p |- ( ( ph /\ ps /\ ch ) -> A. x ( ph /\ ps /\ ch ) ) $=
          ( wa wal w3a hban df-3an albii 3imtr4 ) ABHZCHZPDIABCJZQDIOCDABDEFKGK
          ABCLZQPDRMN $.
          $( [14-Sep-2003] $)
      $}
    $}
  $}

  $( ` x ` is not free in ` -. A. x ph ` . $)
  hbn1 $p |- ( -. A. x ph -> A. x -. A. x ph ) $=
    ( wal hba1 hbn ) ABCBABDE $.
    $( [5-Aug-1993] $)

  $( ` x ` is not free in ` E. x ph ` . $)
  hbe1 $p |- ( E. x ph -> A. x E. x ph ) $=
    ( wn wal wex hbn1 df-ex albii 3imtr4 ) ACZBDCZKBDABEZLBDJBFABGZLKBMHI $.
    $( [5-Aug-1993] $)

  $( Proof of a single axiom that can replace ~ ax-4 and ~ ax-6 .  See
     ~ ax46to4 and ~ ax46to6 for the re-derivation of those axioms.
     (Contributed by Scott Fenton, 12-Sep-2005.) $)
  ax46 $p |- ( ( A. x -. A. x ph -> A. x ph ) -> ph ) $=
    ( wal wn ax-6 ax-4 ja ) ABCZDBCHAABEABFG $.
    $( [19-Sep-2005] $) $( [12-Sep-2005] $)

  $( Re-derivation of ~ ax-4 from ~ ax46 .  Only propositional calculus is used
     for the re-derivation.  (Contributed by Scott Fenton, 12-Sep-2005.) $)
  ax46to4 $p |- ( A. x ph -> ph ) $=
    ( wal wn wi ax-1 ax46 syl ) ABCZIDBCZIEAIJFABGH $.
    $( [13-Sep-2005] $) $( [12-Sep-2005] $)

  $( Re-derivation of ~ ax-6 from ~ ax46 .  Only propositional calculus is used
     for the re-derivation.  (Contributed by Scott Fenton, 12-Sep-2005.) $)
  ax46to6 $p |- ( -. A. x -. A. x ph -> ph ) $=
    ( wal wn wi pm2.21 ax46 syl ) ABCZDBCZDJIEAJIFABGH $.
    $( [13-Sep-2005] $) $( [12-Sep-2005] $)

  $( Proof of a single axiom that can replace both ~ ax-6 and ~ ax-7 .  See
     ~ ax67to6 and ~ ax67to7 for the re-derivation of those axioms. $)
  ax67 $p |- ( -. A. x -. A. y A. x ph -> A. y ph ) $=
    ( wal wn ax-7 con3i 19.20i ax-6 syl ) ABDCDZEZBDZEACDZBDZEZBDZENQMPLBKOACBF
    GHGNBIJ $.
    $( [18-Nov-2006] $) $( [18-Nov-2006] $)

  $( Re-derivation of ~ ax-6 from ~ ax67 .  Note that ~ ax-6 and ~ ax-7 are not
     used by the re-derivation. $)
  ax67to6 $p |- ( -. A. x -. A. x ph -> ph ) $=
    ( wal wn hba1 con3i 19.20i ax67 ax-4 3syl ) ABCZDZBCZDKBCZDZBCZDKAPMOLBKNAB
    EFGFABBHABIJ $.
    $( [19-Nov-2006] $) $( [18-Nov-2006] $)

  $( Re-derivation of ~ ax-7 from ~ ax67 .  Note that ~ ax-6 and ~ ax-7 are not
     used by the re-derivation. $)
  ax67to7 $p |- ( A. x A. y ph -> A. y A. x ph ) $=
    ( wal wn ax67to6 a3i ax67 19.20i syl ) ACDBDZKEZCDEZCDZABDZCDNKLCFGMOCACBHI
    J $.
    $( [19-Nov-2006] $) $( [18-Nov-2006] $)

  $( Proof of a single axiom that can replace ~ ax-4 , ~ ax-6 , and ~ ax-7 .
     See ~ ax467to4 , ~ ax467to6 , and ~ ax467to7 for the re-derivation of
     those axioms.  This theorem extends the idea in Scott Fenton's ~ ax46 . $)
  ax467 $p |- ( ( A. x A. y -. A. x A. y ph -> A. x ph ) -> ph ) $=
    ( wal wn ax-4 hbn1 ax-6 con1i 19.20i ax-7 3syl nsyl4 ja ) ACDZBDEZCDBDZABDA
    OAQACFOEZRCDPBDZCDQACGRSCSOOBHIJPCBKLMABFN $.
    $( [19-Nov-2006] $) $( [18-Nov-2006] $)

  $( Re-derivation of ~ ax-4 from ~ ax467 .  Only propositional calculus is
     used by the re-derivation. $)
  ax467to4 $p |- ( A. x ph -> ph ) $=
    ( wal wn wi ax-1 ax467 syl ) ABCZIBCDBCBCZIEAIJFABBGH $.
    $( [19-Nov-2006] $) $( [19-Nov-2006] $)

  $( Re-derivation of ~ ax-6 from ~ ax467 .  Note that ~ ax-6 and ~ ax-7 are
     not used by the re-derivation.  The use of ~ 19.20i (which uses ~ ax-4 )
     is allowed since we have already proved ~ ax467to4 . $)
  ax467to6 $p |- ( -. A. x -. A. x ph -> ph ) $=
    ( wal wn wi ax467to4 hba1 con3i 19.20i syl pm2.21 ax467 3syl ) ABCZDZBCZDNB
    CZDZBCZBCZDTNEATPTSPSBFROBNQABGHIJHTNKABBLM $.
    $( [19-Nov-2006] $) $( [19-Nov-2006] $)

  $( Re-derivation of ~ ax-7 from ~ ax467 .  Note that ~ ax-6 and ~ ax-7 are
     not used by the re-derivation.  The use of ~ 19.20i (which uses ~ ax-4 )
     is allowed since we have already proved ~ ax467to4 . $)
  ax467to7 $p |- ( A. x A. y ph -> A. y A. x ph ) $=
    ( wal wn ax467to6 a3i wi pm2.21 ax467 syl 19.20i nsyl4 ) ACDBDZNEZCDZEZCDZA
    BDZCDRNOCFGQSCPBDZEZBDSPUAABUATSHATSIABCJKLPBFMLK $.
    $( [19-Nov-2006] $) $( [19-Nov-2006] $)

  $( Axiom C5-2 of [Monk2] p. 113, which we prove from our ~ ax-6 (and others).
     Conversely, we can re-derive ~ ax-6 follows from this one (see
     ~ ax6-3 ), showing that they are interchangeable. $)
  ax6-2 $p |- ( -. A. x ph -> A. x -. A. x ph ) $=
    ( hbn1 ) ABC $.
    $( [2-Dec-2003] $) $( [2-Dec-2003] $)

  $( Re-derive ~ ax-6 from ~ ax6-2 (Axiom C5-2 of [Monk2] p. 113), showing
     that they are equivalent in the presence of the others. $)
  ax6-3 $p |- ( -. A. x -. A. x ph -> ph ) $=
    ( wal wn ax-4 ax6-2 nsyl4 ) ABCZAHDBCABEABFG $.
    $( [17-Nov-2006] $) $( [17-Nov-2006] $)

  $( The analog in our "pure" predicate calculus of axiom 5 of modal logic
     S5. $)
  modal-5 $p |- ( -. A. x -. ph -> A. x -. A. x -. ph ) $=
    ( wn hbn1 ) ACBD $.
    $( [8-Oct-2005] $) $( [5-Oct-2005] $)

  $( The analog in our "pure" predicate calculus of the Brouwer axiom (B) of
     modal logic S5. $)
  modal-b $p |- ( ph -> A. x -. A. x -. ph ) $=
    ( wn wal ax-6 a3i ) ACZBDCBDAGBEF $.
    $( [6-Oct-2005] $) $( [5-Oct-2005] $)

  $( If a wff is true, it is true for at least one instance.  Special case of
     Theorem 19.8 of [Margaris] p. 89. $)
  19.8a $p |- ( ph -> E. x ph ) $=
    ( wn wal wex ax-4 con2i df-ex sylibr ) AACZBDZCABEKAJBFGABHI $.
    $( [5-Aug-1993] $)

  $( Theorem 19.2 of [Margaris] p. 89. $)
  19.2 $p |- ( A. x ph -> E. x ph ) $=
    ( wex 19.8a a4s ) AABCBABDE $.
    $( [5-Aug-1993] $)

  ${
    19.3.1 $e |- ( ph -> A. x ph ) $.
    $( A wff may be quantified with a variable not free in it.  Theorem 19.3 of
       [Margaris] p. 89. $)
    19.3 $p |- ( A. x ph <-> ph ) $=
      ( wal ax-4 impbi ) ABDAABECF $.
      $( [21-May-2007] $) $( [5-Aug-1993] $)
  $}

  $( Theorem 19.5 of [Margaris] p. 89. $)
  alcom $p |- ( A. x A. y ph <-> A. y A. x ph ) $=
    ( wal ax-7 impbi ) ACDBDABDCDABCEACBEF $.
    $( [5-Aug-1993] $)

  $( Theorem 19.7 of [Margaris] p. 89. $)
  alnex $p |- ( A. x -. ph <-> -. E. x ph ) $=
    ( wex wn wal df-ex con2bii ) ABCADBEABFG $.
    $( [5-Aug-1993] $)

  $( Theorem 19.6 of [Margaris] p. 89. $)
  alex $p |- ( A. x ph <-> -. E. x -. ph ) $=
    ( wal wn wex pm4.13 albii alnex bitr ) ABCADZDZBCJBEDAKBAFGJBHI $.
    $( [5-Aug-1993] $)

  $( A closed version of one direction of ~ 19.9 . $)
  19.9t $p |- ( A. x ( ph -> A. x ph ) -> ( E. x ph -> ph ) ) $=
    ( wal wi wn wex hbnt con1d df-ex syl5ib ) AABCDBCZAEBCZEAABFKALABGHABIJ $.
    $( [5-Aug-1993] $)

  ${
    19.9.1 $e |- ( ph -> A. x ph ) $.
    $( A wff may be existentially quantified with a variable not free in it.
       Theorem 19.9 of [Margaris] p. 89.  (Contributed by FL, 24-Mar-2007.) $)
    19.9 $p |- ( E. x ph <-> ph ) $=
      ( wex wal wi 19.9t mpg 19.8a impbi ) ABDZAAABEFKAFBABGCHABIJ $.
      $( [20-May-2007] $) $( [24-Mar-2007] $)
  $}

  ${
    19.9d.1 $e |- ( ps -> A. x ps ) $.
    19.9d.2 $e |- ( ps -> ( ph -> A. x ph ) ) $.
    $( A deduction version of one direction of ~ 19.9 . $)
    19.9d $p |- ( ps -> ( E. x ph -> ph ) ) $=
      ( wal wi wex 19.20i 19.9t 3syl ) BBCFAACFGZCFACHAGDBLCEIACJK $.
      $( [5-Aug-1993] $)
  $}

  $( Theorem 19.14 of [Margaris] p. 90. $)
  exnal $p |- ( E. x -. ph <-> -. A. x ph ) $=
    ( wal wn wex alex con2bii ) ABCADBEABFG $.
    $( [5-Aug-1993] $)

  $( Theorem 19.22 of [Margaris] p. 90. $)
  19.22 $p |- ( A. x ( ph -> ps ) -> ( E. x ph -> E. x ps ) ) $=
    ( wi wal wn wex con3 19.20ii con3d df-ex 3imtr4g ) ABDZCEZAFZCEZFBFZCEZFACG
    BCGNRPMQOCABHIJACKBCKL $.
    $( [5-Aug-1993] $)

  ${
    19.22i.1 $e |- ( ph -> ps ) $.
    $( Inference adding existential quantifier to antecedent and consequent. $)
    19.22i $p |- ( E. x ph -> E. x ps ) $=
      ( wi wex 19.22 mpg ) ABEACFBCFECABCGDH $.
      $( [5-Aug-1993] $)

    $( Inference adding 2 existential quantifiers to antecedent and
       consequent. $)
    19.22i2 $p |- ( E. x E. y ph -> E. x E. y ps ) $=
      ( wex 19.22i ) ADFBDFCABDEGG $.
      $( [5-Feb-2005] $) $( [3-Feb-2005] $)
  $}

  $( A transformation of quantifiers and logical connectives. $)
  alinexa $p |- ( A. x ( ph -> -. ps ) <-> -. E. x ( ph /\ ps ) ) $=
    ( wn wi wal wa wex imnan albii alnex bitr ) ABDEZCFABGZDZCFNCHDMOCABIJNCKL
    $.
    $( [19-Aug-1993] $)

  $( A transformation of quantifiers and logical connectives. $)
  exanali $p |- ( E. x ( ph /\ -. ps ) <-> -. A. x ( ph -> ps ) ) $=
    ( wi wal wn wa wex iman albii alnex bitr con2bii ) ABDZCEZABFGZCHZOPFZCEQFN
    RCABIJPCKLM $.
    $( [25-Mar-1996] $)

  $( A relationship between two quantifiers and negation. $)
  alexn $p |- ( A. x E. y -. ph <-> -. E. x A. y ph ) $=
    ( wn wex wal exnal albii alnex bitr ) ADCEZBFACFZDZBFLBEDKMBACGHLBIJ $.
    $( [18-Aug-1993] $)

  $( One direction of Theorem 19.11 of [Margaris] p. 89. $)
  excomim $p |- ( E. x E. y ph -> E. y E. x ph ) $=
    ( wex 19.8a 19.22i2 hbe1 hbex 19.9 sylib ) ACDBDABDZCDZBDLAKBCABEFLBKBCABGH
    IJ $.
    $( [5-Aug-1993] $)

  $( Theorem 19.11 of [Margaris] p. 89. $)
  excom $p |- ( E. x E. y ph <-> E. y E. x ph ) $=
    ( wex excomim impbi ) ACDBDABDCDABCEACBEF $.
    $( [5-Aug-1993] $)

  $( Theorem 19.12 of [Margaris] p. 89.  Assuming the converse is a mistake
     sometimes made by beginners!  But sometimes the converse does hold,
     as in ~ 19.12vv . $)
  19.12 $p |- ( E. x A. y ph -> A. y E. x ph ) $=
    ( wal wex hba1 hbex ax-4 19.22i 19.21ai ) ACDZBEABECKCBACFGKABACHIJ $.
    $( [5-Aug-1993] $)

  ${
    19.16.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.16 of [Margaris] p. 90. $)
    19.16 $p |- ( A. x ( ph <-> ps ) -> ( ph <-> A. x ps ) ) $=
      ( wb wal 19.15 19.3 syl5bbr ) ABECFACFBCFAABCGACDHI $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.17.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.17 of [Margaris] p. 90. $)
    19.17 $p |- ( A. x ( ph <-> ps ) -> ( A. x ph <-> ps ) ) $=
      ( wb wal 19.15 19.3 syl6bb ) ABECFACFBCFBABCGBCDHI $.
      $( [5-Aug-1993] $)
  $}

  $( Theorem 19.18 of [Margaris] p. 90. $)
  19.18 $p |- ( A. x ( ph <-> ps ) -> ( E. x ph <-> E. x ps ) ) $=
    ( wb wal wex wi bi1 19.20i 19.22 syl bi2 impbid ) ABDZCEZACFZBCFZOABGZCEPQG
    NRCABHIABCJKOBAGZCEQPGNSCABLIBACJKM $.
    $( [5-Aug-1993] $)

  ${
    exbii.1 $e |- ( ph <-> ps ) $.
    $( Inference adding existential quantifier to both sides of an
       equivalence. $)
    exbii $p |- ( E. x ph <-> E. x ps ) $=
      ( wb wex 19.18 mpg ) ABEACFBCFECABCGDH $.
      $( [24-May-1994] $)
  $}

  ${
    2exbii.1 $e |- ( ph <-> ps ) $.
    $( Inference adding 2 existential quantifiers to both sides of an
       equivalence. $)
    2exbii $p |- ( E. x E. y ph <-> E. x E. y ps ) $=
      ( wex exbii ) ADFBDFCABDEGG $.
      $( [16-Mar-1995] $)
  $}

  ${
    3exbii.1 $e |- ( ph <-> ps ) $.
    $( Inference adding 3 existential quantifiers to both sides of an
       equivalence. $)
    3exbi $p |- ( E. x E. y E. z ph <-> E. x E. y E. z ps ) $=
      ( wex exbii 2exbii ) AEGBEGCDABEFHI $.
      $( [2-May-1995] $)
  $}

  $( Commutation of conjunction inside an existential quantifier. $)
  exancom $p |- ( E. x ( ph /\ ps ) <-> E. x ( ps /\ ph ) ) $=
    ( wa ancom exbii ) ABDBADCABEF $.
    $( [18-Aug-1993] $)

  ${
    19.19.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.19 of [Margaris] p. 90. $)
    19.19 $p |- ( A. x ( ph <-> ps ) -> ( ph <-> E. x ps ) ) $=
      ( wb wal wex 19.18 19.9 syl5bbr ) ABECFACGBCGAABCHACDIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.21.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.21 of [Margaris] p. 90.  The hypothesis can be thought of as
       " ` x ` is not free in ` ph ` ." $)
    19.21 $p |- ( A. x ( ph -> ps ) <-> ( ph -> A. x ps ) ) $=
      ( wi wal 19.20 syl5 hba1 hbim ax-4 imim2i 19.21ai impbi ) ABEZCFZABCFZEZP
      ACFQAABCGDHROCAQCDBCIJQBABCKLMN $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.21-2.1 $e |- ( ph -> A. x ph ) $.
    19.21-2.2 $e |- ( ph -> A. y ph ) $.
    $( Theorem 19.21 of [Margaris] p. 90 but with 2 quantifiers. $)
    19.21-2 $p |- ( A. x A. y ( ph -> ps ) <-> ( ph -> A. x A. y ps ) ) $=
      ( wi wal 19.21 albii bitr ) ABGDHZCHABDHZGZCHAMCHGLNCABDFIJAMCEIK $.
      $( [11-Feb-2005] $) $( [4-Feb-2005] $)
  $}

  ${
    stdpc5.1 $e |- ( ph -> A. x ph ) $.
    $( An axiom scheme of standard predicate calculus that emulates Axiom 5 of
       [Mendelson] p. 69.  The hypothesis ` ( ph -> A. x ph ) ` can be thought
       of as emulating " ` x ` is not free in ` ph ` ."  With this convention,
       the meaning of "not free" is less restrictive than the usual textbook
       definition; for example ` x ` would not (for us) be free in ` x = x ` by
       ~ hbequid .  This theorem scheme can be proved as a metatheorem of
       Mendelson's axiom system, even though it is slightly stronger than his
       Axiom 5. $)
    stdpc5 $p |- ( A. x ( ph -> ps ) -> ( ph -> A. x ps ) ) $=
      ( wi wal 19.21 biimp ) ABECFABCFEABCDGH $.
      $( [22-Sep-1993] $)
  $}

  ${
    19.21ad.1 $e |- ( ph -> A. x ph ) $.
    19.21ad.2 $e |- ( ps -> A. x ps ) $.
    19.21ad.3 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.21 of [Margaris] p. 90. $)
    19.21ad $p |- ( ph -> ( ps -> A. x ch ) ) $=
      ( wal wa hban imp 19.21ai ex ) ABCDHABICDABDEFJABCGKLM $.
      $( [10-Feb-1997] $)
  $}

  ${
    19.21bi.1 $e |- ( ph -> A. x ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90. $)
    19.21bi $p |- ( ph -> ps ) $=
      ( wal ax-4 syl ) ABCEBDBCFG $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.21bbi.1 $e |- ( ph -> A. x A. y ps ) $.
    $( Inference removing double quantifier. $)
    19.21bbi $p |- ( ph -> ps ) $=
      ( wal 19.21bi ) ABDABDFCEGG $.
      $( [20-Apr-1994] $)
  $}

  ${
    19.22d.1 $e |- ( ph -> A. x ph ) $.
    19.22d.2 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.22 of [Margaris] p. 90. $)
    19.22d $p |- ( ph -> ( E. x ps -> E. x ch ) ) $=
      ( wi wal wex 19.21ai 19.22 syl ) ABCGZDHBDICDIGAMDEFJBCDKL $.
      $( [20-May-1996] $)
  $}

  ${
    19.23.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.23 of [Margaris] p. 90. $)
    19.23 $p |- ( A. x ( ph -> ps ) <-> ( E. x ph -> ps ) ) $=
      ( wi wal wex 19.22 19.9 syl6ib hbe1 hbim 19.8a imim1i 19.21ai impbi ) ABE
      ZCFZACGZBEZRSBCGBABCHBCDIJTQCSBCACKDLASBACMNOP $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.23ai.1 $e |- ( ps -> A. x ps ) $.
    19.23ai.2 $e |- ( ph -> ps ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90. $)
    19.23ai $p |- ( E. x ph -> ps ) $=
      ( wex 19.22i 19.9 sylib ) ACFBCFBABCEGBCDHI $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.23bi.1 $e |- ( E. x ph -> ps ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90. $)
    19.23bi $p |- ( ph -> ps ) $=
      ( wex 19.8a syl ) AACEBACFDG $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.23ad.1 $e |- ( ph -> A. x ph ) $.
    19.23ad.2 $e |- ( ch -> A. x ch ) $.
    19.23ad.3 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.23 of [Margaris] p. 90. $)
    19.23ad $p |- ( ph -> ( E. x ps -> ch ) ) $=
      ( wi wal wex 19.21ai 19.23 sylib ) ABCHZDIBDJCHANDEGKBCDFLM $.
      $( [28-Jan-1997] $)
  $}

  $( Theorem 19.26 of [Margaris] p. 90.  Also Theorem *10.22 of
     [WhiteheadRussell] p. 119. $)
  19.26 $p |- ( A. x ( ph /\ ps ) <-> ( A. x ph /\ A. x ps ) ) $=
    ( wa wal pm3.26 19.20i pm3.27 jca pm3.2 19.20ii imp impbi ) ABDZCEZACEZBCEZ
    DOPQNACABFGNBCABHGIPQOABNCABJKLM $.
    $( [5-Aug-1993] $)

  $( Theorem 19.26 of [Margaris] p. 90 with two quantifiers. $)
  19.26-2 $p |- ( A. x A. y ( ph /\ ps ) <->
                ( A. x A. y ph /\ A. x A. y ps ) ) $=
    ( wa wal 19.26 albii bitr ) ABEDFZCFADFZBDFZEZCFKCFLCFEJMCABDGHKLCGI $.
    $( [27-Apr-2005] $) $( [3-Feb-2005] $)

  ${
    19.27.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.27 of [Margaris] p. 90. $)
    19.27 $p |- ( A. x ( ph /\ ps ) <-> ( A. x ph /\ ps ) ) $=
      ( wa wal 19.26 19.3 anbi2i bitr ) ABECFACFZBCFZEKBEABCGLBKBCDHIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.28.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.28 of [Margaris] p. 90. $)
    19.28 $p |- ( A. x ( ph /\ ps ) <-> ( ph /\ A. x ps ) ) $=
      ( wa wal 19.26 19.3 anbi1i bitr ) ABECFACFZBCFZEALEABCGKALACDHIJ $.
      $( [5-Aug-1993] $)
  $}

  $( Theorem 19.29 of [Margaris] p. 90. $)
  19.29 $p |- ( ( A. x ph /\ E. x ps ) -> E. x ( ph /\ ps ) ) $=
    ( wal wex wa wn wi 19.20 alnex syl6ib con3i df-an exnal 3imtr4 exbii
    sylibr ) ACDZBCEZFZABGZHZGZCEZABFZCERSGZHZGUBCDZGTUDUHUGUHRUACDUFAUACIBCJKL
    RSMUBCNOUEUCCABMPQ $.
    $( [5-Aug-1993] $)

  $( Variation of Theorem 19.29 of [Margaris] p. 90. $)
  19.29r $p |- ( ( E. x ph /\ A. x ps ) -> E. x ( ph /\ ps ) ) $=
    ( wal wex wa 19.29 ancom exancom 3imtr4 ) BCDZACEZFBAFCELKFABFCEBACGLKHABCI
    J $.
    $( [18-Aug-1993] $)

  $( Variation of Theorem 19.29 of [Margaris] p. 90 with double
     quantification. $)
  19.29r2 $p |- ( ( E. x E. y ph /\ A. x A. y ps ) ->
             E. x E. y ( ph /\ ps ) ) $=
    ( wex wal wa 19.29r 19.22i syl ) ADEZCEBDFZCFGKLGZCEABGDEZCEKLCHMNCABDHIJ
    $.
    $( [11-Feb-2005] $) $( [3-Feb-2005] $)

  $( Variation of Theorem 19.29 of [Margaris] p. 90 with mixed
     quantification. $)
  19.29x $p |- ( ( E. x A. y ph /\ A. x E. y ps ) ->
             E. x E. y ( ph /\ ps ) ) $=
    ( wal wex wa 19.29r 19.29 19.22i syl ) ADEZCFBDFZCEGLMGZCFABGDFZCFLMCHNOCAB
    DIJK $.
    $( [25-Mar-2005] $) $( [11-Feb-2005] $)

  $( Theorem 19.35 of [Margaris] p. 90.  This theorem is useful for moving
     an implication (in the form of the right-hand side) into the scope of a
     single existential quantifier. $)
  19.35 $p |- ( E. x ( ph -> ps ) <-> ( A. x ph -> E. x ps ) ) $=
    ( wal wn wi wex wa 19.26 annim albii df-an 3bitr3 con2bii df-ex imbi2i
    3bitr4r ) ACDZBEZCDZEZFZABFZEZCDZERBCGZFUCCGUEUBASHZCDRTHUEUBEASCIUGUDCABJK
    RTLMNUFUARBCOPUCCOQ $.
    $( [5-Aug-1993] $)

  ${
    19.35i.1 $e |- E. x ( ph -> ps ) $.
    $( Inference from Theorem 19.35 of [Margaris] p. 90. $)
    19.35i $p |- ( A. x ph -> E. x ps ) $=
      ( wi wex wal 19.35 mpbi ) ABECFACGBCFEDABCHI $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.35ri.1 $e |- ( A. x ph -> E. x ps ) $.
    $( Inference from Theorem 19.35 of [Margaris] p. 90. $)
    19.35ri $p |- E. x ( ph -> ps ) $=
      ( wi wex wal 19.35 mpbir ) ABECFACGBCFEDABCHI $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.36.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.36 of [Margaris] p. 90. $)
    19.36 $p |- ( E. x ( ph -> ps ) <-> ( A. x ph -> ps ) ) $=
      ( wi wex wal 19.35 19.9 imbi2i bitr ) ABECFACGZBCFZELBEABCHMBLBCDIJK $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.36i.1 $e |- ( ps -> A. x ps ) $.
    19.36i.2 $e |- E. x ( ph -> ps ) $.
    $( Inference from Theorem 19.36 of [Margaris] p. 90. $)
    19.36i $p |- ( A. x ph -> ps ) $=
      ( wi wex wal 19.36 mpbi ) ABFCGACHBFEABCDIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.37.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.37 of [Margaris] p. 90. $)
    19.37 $p |- ( E. x ( ph -> ps ) <-> ( ph -> E. x ps ) ) $=
      ( wi wex wal 19.35 19.3 imbi1i bitr ) ABECFACGZBCFZEAMEABCHLAMACDIJK $.
      $( [5-Aug-1993] $)
  $}

  $( Theorem 19.38 of [Margaris] p. 90. $)
  19.38 $p |- ( ( E. x ph -> A. x ps ) -> A. x ( ph -> ps ) ) $=
    ( wex wal wi hbe1 hba1 hbim 19.8a ax-4 imim12i 19.21ai ) ACDZBCEZFABFCNOCAC
    GBCHIANOBACJBCKLM $.
    $( [5-Aug-1993] $)

  $( Theorem 19.39 of [Margaris] p. 90. $)
  19.39 $p |- ( ( E. x ph -> E. x ps ) -> E. x ( ph -> ps ) ) $=
    ( wex wi wal 19.2 imim1i 19.35 sylibr ) ACDZBCDZEACFZLEABECDMKLACGHABCIJ $.
    $( [5-Aug-1993] $)

  $( Theorem 19.24 of [Margaris] p. 90. $)
  19.24 $p |- ( ( A. x ph -> A. x ps ) -> E. x ( ph -> ps ) ) $=
    ( wal wi wex 19.2 imim2i 19.35 sylibr ) ACDZBCDZEKBCFZEABECFLMKBCGHABCIJ $.
    $( [5-Aug-1993] $)

  $( Theorem 19.25 of [Margaris] p. 90. $)
  19.25 $p |- ( A. y E. x ( ph -> ps ) ->
              ( E. y A. x ph -> E. y E. x ps ) ) $=
    ( wi wex wal 19.35 biimp 19.20i 19.22 syl ) ABECFZDGACGZBCFZEZDGNDFODFEMPDM
    PABCHIJNODKL $.
    $( [5-Aug-1993] $)

  $( Theorem 19.30 of [Margaris] p. 90. $)
  19.30 $p |- ( A. x ( ph \/ ps ) -> ( A. x ph \/ E. x ps ) ) $=
    ( wn wi wal wo wex 19.20 orcom df-or bitr albii df-ex orbi2i imor 3bitr4
    3imtr4 ) BDZAEZCFSCFZACFZEZABGZCFUBBCHZGZSACIUDTCUDBAGTABJBAKLMUBUADZGUGUBG
    UFUCUBUGJUEUGUBBCNOUAUBPQR $.
    $( [5-Aug-1993] $)

  ${
    19.32.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.32 of [Margaris] p. 90. $)
    19.32 $p |- ( A. x ( ph \/ ps ) <-> ( ph \/ A. x ps ) ) $=
      ( wn wi wal wo hbn 19.21 df-or albii 3bitr4 ) AEZBFZCGNBCGZFABHZCGAPHNBCA
      CDIJQOCABKLAPKM $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.31.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.31 of [Margaris] p. 90. $)
    19.31 $p |- ( A. x ( ph \/ ps ) <-> ( A. x ph \/ ps ) ) $=
      ( wo wal 19.32 orcom albii 3bitr4 ) BAEZCFBACFZEABEZCFLBEBACDGMKCABHILBHJ
      $.
      $( [5-Aug-1993] $)
  $}

  $( Theorem 19.43 of [Margaris] p. 90. $)
  19.43 $p |- ( E. x ( ph \/ ps ) <-> ( E. x ph \/ E. x ps ) ) $=
    ( wo wn wal wex wa ioran albii 19.26 alnex anbi12i 3bitr negbii df-ex oran
    3bitr4 ) ABDZEZCFZEACGZEZBCGZEZHZESCGUBUDDUAUFUAAEZBEZHZCFUGCFZUHCFZHUFTUIC
    ABIJUGUHCKUJUCUKUEACLBCLMNOSCPUBUDQR $.
    $( [5-Aug-1993] $)

  ${
    19.44.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.44 of [Margaris] p. 90. $)
    19.44 $p |- ( E. x ( ph \/ ps ) <-> ( E. x ph \/ ps ) ) $=
      ( wo wex 19.43 19.9 orbi2i bitr ) ABECFACFZBCFZEKBEABCGLBKBCDHIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.45.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.45 of [Margaris] p. 90. $)
    19.45 $p |- ( E. x ( ph \/ ps ) <-> ( ph \/ E. x ps ) ) $=
      ( wo wex 19.43 19.9 orbi1i bitr ) ABECFACFZBCFZEALEABCGKALACDHIJ $.
      $( [5-Aug-1993] $)
  $}

  $( Theorem 19.33 of [Margaris] p. 90. $)
  19.33 $p |- ( ( A. x ph \/ A. x ps ) -> A. x ( ph \/ ps ) ) $=
    ( wal wo orc 19.20i olc jaoi ) ACDABEZCDBCDAJCABFGBJCBAHGI $.
    $( [5-Aug-1993] $)

  $( The antecedent provides a condition implying the converse of ~ 19.33 .
     Compare Theorem 19.33 of [Margaris] p. 90. $)
  19.33b $p |- ( -. ( E. x ph /\ E. x ps ) ->
               (  A. x ( ph \/ ps ) <-> ( A. x ph \/ A. x ps ) ) ) $=
    ( wex wa wn wo wal wi ianor alnex orbi12i bitr4 wb biorf 19.20i 19.15 syl
    olc syl6bir orcom syl6bb orc jaoi sylbi 19.33 impbid1 ) ACDZBCDZEFZABGZCHZA
    CHZBCHZGZUJAFZCHZBFZCHZGZULUOIZUJUHFZUIFZGUTUHUIJUQVBUSVCACKBCKLMUQVAUSUQUL
    UNUOUQBUKNZCHUNULNUPVDCABOPBUKCQRUNUMSTUSULUMUOUSAUKNZCHUMULNURVECURABAGUKB
    AOBAUAUBPAUKCQRUMUNUCTUDUEABCUFUG $.
    $( [2-Apr-2004] $) $( [27-Mar-2004] $)

  $( Theorem 19.34 of [Margaris] p. 90. $)
  19.34 $p |- ( ( A. x ph \/ E. x ps ) -> E. x ( ph \/ ps ) ) $=
    ( wal wex wo 19.2 orim1i 19.43 sylibr ) ACDZBCEZFACEZLFABFCEKMLACGHABCIJ $.
    $( [5-Aug-1993] $)

  $( Theorem 19.40 of [Margaris] p. 90. $)
  19.40 $p |- ( E. x ( ph /\ ps ) -> ( E. x ph /\ E. x ps ) ) $=
    ( wa wex pm3.26 19.22i pm3.27 jca ) ABDZCEACEBCEJACABFGJBCABHGI $.
    $( [5-Aug-1993] $)

  ${
    19.41.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.41 of [Margaris] p. 90. $)
    19.41 $p |- ( E. x ( ph /\ ps ) <-> ( E. x ph /\ ps ) ) $=
      ( wa wex wn wal df-ex wo hbn 19.31 ianor albii alnex orbi1i bitr4 3bitr4
      con2bii ) ABEZCFTGZCHZGACFZBEZTCIUBUDAGZBGZJZCHUECHZUFJZUBUDGZUEUFCBCDKLU
      AUGCABMNUJUCGZUFJUIUCBMUHUKUFACOPQRSQ $.
      $( [5-Aug-1993] $)
  $}

  ${
    19.42.1 $e |- ( ph -> A. x ph ) $.
    $( Theorem 19.42 of [Margaris] p. 90. $)
    19.42 $p |- ( E. x ( ph /\ ps ) <-> ( ph /\ E. x ps ) ) $=
      ( wa wex 19.41 exancom ancom 3bitr4 ) BAECFBCFZAEABECFAKEBACDGABCHAKIJ $.
      $( [18-Aug-1993] $)
  $}

  $( Rotate 4 universal quantifiers twice. $)
  alrot4 $p |- ( A. x A. y A. z A. w ph <-> A. z A. w A. x A. y ph ) $=
    ( wal alcom albii bitr 3bitr ) AEFZDFCFZBFACFZEFZDFZBFNBFZDFMBFEFZDFLOBLKCF
    ZDFOKCDGRNDACEGHIHNBDGPQDMBEGHJ $.
    $( [7-Feb-2005] $) $( [2-Feb-2005] $)

  $( Swap 1st and 3rd existential quantifiers. $)
  excom13 $p |- ( E. x E. y E. z ph <-> E. z E. y E. x ph ) $=
    ( wex excom exbii 3bitr ) ADEZCEBEIBEZCEABEZDEZCEKCEDEIBCFJLCABDFGKCDFH $.
    $( [9-Mar-1995] $)

  $( Rotate existential quantifiers. $)
  exrot3 $p |- ( E. x E. y E. z ph <-> E. y E. z E. x ph ) $=
    ( wex excom13 excom bitr ) ADECEBEABEZCEDEIDECEABCDFIDCGH $.
    $( [17-Mar-1995] $)

  $( Rotate existential quantifiers twice. $)
  exrot4 $p |- ( E. x E. y E. z E. w ph <-> E. z E. w E. x E. y ph ) $=
    ( wex excom13 exbii bitr ) AEFDFCFZBFACFZDFEFZBFKBFEFDFJLBACDEGHKBEDGI $.
    $( [9-Mar-1995] $)

  ${
    nex.1 $e |- -. ph $.
    $( Generalization rule for negated wff. $)
    nex $p |- -. E. x ph $=
      ( wn wex alnex mpgbi ) ADABEDBABFCG $.
      $( [18-May-1994] $)
  $}

  ${
    nexd.1 $e |- ( ph -> A. x ph ) $.
    nexd.2 $e |- ( ph -> -. ps ) $.
    $( Deduction for generalization rule for negated wff. $)
    nexd $p |- ( ph -> -. E. x ps ) $=
      ( wn wal wex 19.21ai alnex sylib ) ABFZCGBCHFALCDEIBCJK $.
      $( [2-Jan-2002] $)
  $}

  ${
    hbim1.1 $e |- ( ph -> A. x ph ) $.
    hbim1.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( A closed form of ~ hbim . $)
    hbim1 $p |- ( ( ph -> ps ) -> A. x ( ph -> ps ) ) $=
      ( wi wal a2i 19.21 sylibr ) ABFZABCGZFKCGABLEHABCDIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    albid.1 $e |- ( ph -> A. x ph ) $.
    albid.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for universal quantifier (deduction rule). $)
    albid $p |- ( ph -> ( A. x ps <-> A. x ch ) ) $=
      ( wb wal 19.21ai 19.15 syl ) ABCGZDHBDHCDHGALDEFIBCDJK $.
      $( [5-Aug-1993] $)
  $}

  ${
    exbid.1 $e |- ( ph -> A. x ph ) $.
    exbid.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for existential quantifier (deduction rule). $)
    exbid $p |- ( ph -> ( E. x ps <-> E. x ch ) ) $=
      ( wb wal wex 19.21ai 19.18 syl ) ABCGZDHBDICDIGAMDEFJBCDKL $.
      $( [5-Aug-1993] $)
  $}

  ${
    exan.1 $e |- ( E. x ph /\ ps ) $.
    $( Place a conjunct in the scope of an existential quantifier. $)
    exan $p |- E. x ( ph /\ ps ) $=
      ( wa wex wal hbe1 19.27 ancom mpbi mpgbi 19.29 ax-mp exancom ) BAECFZABEC
      FBCGACFZEZPBQEZRCBQCACHIQBESDQBJKLBACMNBACOK $.
      $( [18-Aug-1993] $)
  $}

  $( Split a biconditional and distribute quantifier. $)
  albi $p |- ( A. x ( ph <-> ps ) <->
             ( A. x ( ph -> ps ) /\ A. x ( ps -> ph ) ) ) $=
    ( wb wal wi wa bi albii 19.26 bitr ) ABDZCEABFZBAFZGZCEMCENCEGLOCABHIMNCJK
    $.
    $( [18-Aug-1993] $)

  $( Split a biconditional and distribute 2 quantifiers. $)
  2albi $p |- ( A. x A. y ( ph <-> ps ) <->
             ( A. x A. y ( ph -> ps ) /\ A. x A. y ( ps -> ph ) ) ) $=
    ( wb wal wi wa albi albii 19.26 bitr ) ABEDFZCFABGDFZBAGDFZHZCFNCFOCFHMPCAB
    DIJNOCKL $.
    $( [5-Feb-2005] $) $( [3-Feb-2005] $)

  ${
    hbnd.1 $e |- ( ph -> A. x ph ) $.
    hbnd.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hbn . $)
    hbnd $p |- ( ph -> ( -. ps -> A. x -. ps ) ) $=
      ( wal wi wn 19.21ai hbnt syl ) ABBCFGZCFBHZMCFGALCDEIBCJK $.
      $( [3-Jan-2002] $)
  $}

  ${
    hbimd.1 $e |- ( ph -> A. x ph ) $.
    hbimd.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hbimd.3 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hbim . $)
    hbimd $p |- ( ph -> ( ( ps -> ch ) -> A. x ( ps -> ch ) ) ) $=
      ( wi wal wn hbnd pm2.21 19.20i syl6com ax-1 ja com12 ) BCHZARDIZBCASHABJZ
      TDISABDEFKTRDBCLMNACCDISGCRDCBOMNPQ $.
      $( [1-Jan-2002] $)
  $}

  ${
    hbimOLD.1 $e |- ( ph -> A. x ph ) $.
    hbimOLD.2 $e |- ( ps -> A. x ps ) $.
    $( If ` x ` is not free in ` ph ` and ` ps ` , it is not free in
       ` ( ph -> ps ) ` . $)
    hbimOLD $p |- ( ( ph -> ps ) -> A. x ( ph -> ps ) ) $=
      ( wi wal id hbth imim2i a1i hbimd ax-mp ) AAFZABFZOCGFAHZNABCNCPIAACGADJB
      BCGFNEKLM $.
      $( [5-Aug-1993] $)
  $}

  ${
    hband.1 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hband.2 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hban . $)
    hband $p |- ( ph -> ( ( ps /\ ch ) -> A. x ( ps /\ ch ) ) ) $=
      ( wa wal anim12d 19.26 syl6ibr ) ABCGZBDHZCDHZGLDHABMCNEFIBCDJK $.
      $( [2-Jan-2002] $)
  $}

  ${
    hbbid.1 $e |- ( ph -> A. x ph ) $.
    hbbid.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hbbid.3 $e |- ( ph -> ( ch -> A. x ch ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hbbi . $)
    hbbid $p |- ( ph -> ( ( ps <-> ch ) -> A. x ( ps <-> ch ) ) ) $=
      ( wi wa wal wb hbimd anim12d bi albi 3imtr4g ) ABCHZCBHZIQDJZRDJZIBCKZUAD
      JAQSRTABCDEFGLACBDEGFLMBCNBCDOP $.
      $( [1-Jan-2002] $)
  $}

  ${
    hbald.1 $e |- ( ph -> A. y ph ) $.
    hbald.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hbal . $)
    hbald $p |- ( ph -> ( A. y ps -> A. x A. y ps ) ) $=
      ( wal 19.20d ax-7 syl6 ) ABDGZBCGZDGKCGABLDEFHBDCIJ $.
      $( [2-Jan-2002] $)
  $}

  ${
    hbexd.1 $e |- ( ph -> A. y ph ) $.
    hbexd.2 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( Deduction form of bound-variable hypothesis builder ~ hbex . $)
    hbexd $p |- ( ph -> ( E. y ps -> A. x E. y ps ) ) $=
      ( wex wal 19.22d 19.12 syl6 ) ABDGZBCHZDGLCHABMDEFIBDCJK $.
      $( [2-Jan-2002] $)
  $}

  $( Closed form of Theorem 19.21 of [Margaris] p. 90. $)
  19.21t $p |- ( A. x ( ph -> A. x ph ) ->
               ( A. x ( ph -> ps ) <-> ( ph -> A. x ps ) ) ) $=
    ( wal wi 19.20 imim2d com12 a4s hba1 ax-4 a1i hbimd imim2i 19.20i syl6
    impbid ) AACDZEZCDZABEZCDZABCDZEZSUBUDECUBSUDUBRUCAABCFGHITUDUDCDUBTAUCCSCJ
    SCKUCUCCDETBCJLMUDUACUCBABCKNOPQ $.
    $( [27-May-1997] $)

  $( Closed form of Theorem 19.23 of [Margaris] p. 90. $)
  19.23t $p |- ( A. x ( ps -> A. x ps ) ->
               ( A. x ( ph -> ps ) <-> ( E. x ph -> ps ) ) ) $=
    ( wal wi wex hba1 ax-4 impbid2 imbi2d albid 19.23 syl5bb bitr3d ) BBCDZEZCD
    ZAOEZCDZABEZCDACFZBEZQRTCPCGQOBAQOBBCHPCHIZJKQUAOEUBSQOBUAUCJAOCBCGLMN $.
    $( [8-Nov-2005] $) $( [7-Nov-2005] $)

  $( Introduce a conjunct in the scope of an existential quantifier. $)
  exintr $p |- ( A. x ( ph -> ps ) -> ( E. x ph -> E. x ( ph /\ ps ) ) ) $=
    ( wi wal wa hba1 ancl a4s 19.22d ) ABDZCEAABFZCKCGKALDCABHIJ $.
    $( [11-Aug-1993] $)

  $( Add/remove a conjunct in the scope of an existential quantifier.
     (Contributed by Raph Levien, 3-Jul-2006.) $)
  exintrbi $p |- ( A. x ( ph -> ps ) -> ( E. x ph <-> E. x ( ph /\ ps ) ) ) $=
    ( wi wal wa wb wex pm4.71 albii 19.18 sylbi ) ABDZCEAABFZGZCEACHNCHGMOCABIJ
    ANCKL $.
    $( [6-Jul-2006] $) $( [3-Jul-2006] $)

  ${
    aaan.1 $e |- ( ph -> A. y ph ) $.
    aaan.2 $e |- ( ps -> A. x ps ) $.
    $( Rearrange universal quantifiers. $)
    aaan $p |- ( A. x A. y ( ph /\ ps ) <-> ( A. x ph /\ A. y ps ) ) $=
      ( wa wal 19.28 albii hbal 19.27 bitr ) ABGDHZCHABDHZGZCHACHOGNPCABDEIJAOC
      BCDFKLM $.
      $( [12-Aug-1993] $)
  $}

  ${
    eeor.1 $e |- ( ph -> A. y ph ) $.
    eeor.2 $e |- ( ps -> A. x ps ) $.
    $( Rearrange existential quantifiers. $)
    eeor $p |- ( E. x E. y ( ph \/ ps ) <-> ( E. x ph \/ E. y ps ) ) $=
      ( wo wex 19.45 exbii hbex 19.44 bitr ) ABGDHZCHABDHZGZCHACHOGNPCABDEIJAOC
      BCDFKLM $.
      $( [8-Aug-1994] $)
  $}

  $( Quantified "excluded middle."  Exercise 9.2a of Boolos, p. 111,
     _Computability and Logic_. $)
  qexmid $p |- E. x ( ph -> A. x ph ) $=
    ( wal 19.8a 19.35ri ) AABCZBFBDE $.
    $( [10-Dec-2000] $)

