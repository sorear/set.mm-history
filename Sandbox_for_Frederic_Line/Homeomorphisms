$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( In this section every @d statement has been checked. 10-Feb-2008 $)

  $c Homeo $. $( Class of all homeomorphisms. $)
  $c ~= $. $( The relation "is homeomorph to." $)

  $( Extend class notation with the class of all homeomorphisms. $)
  chomeosm $a class Homeo $.

  $( Extend class notation with the relation "is homeomorph to.". $)
  chomeo $a class ~= $.

  ${
    $d j k z f x $.
    $( Function returning all the  homeomorphisms from topology ` j `
       to topology ` k ` . $)
    df-homeo $a |-  Homeo = { <. <. j , k >. , z >. | ( ( j e. Top /\
     k e. Top ) /\  z = { f | ( f : U. j -1-1-onto-> U. k /\
     A. x e. j ( f " x ) e. k /\
     A. x e. k ( `' f " x ) e. j ) } ) } $.
  $}

  ${ $d J f j k x z $. $d K f j k x z $. $d X f j k z $. $d Y f k z $.
    homeofval.1 $e |- X = U. J $.
    homeofval.2 $e |- Y = U. K $.
    $( The set of all the homeomorphisms between two topologies. $)
    homeofval $p |- ( ( J e. Top /\ K e. Top ) -> ( J Homeo K ) =
    { f | ( f : X -1-1-onto-> Y /\ A. x e. J ( f " x ) e. K /\
    A. x e. K ( `' f " x ) e. J ) } ) $=
      ( vj vk vz ctop wcel wa cv wf1o cima wral ccnv w3a cab cvv chomeosm co
      wceq wf wss ssexg wi wal f1of 3ad2ant1 a1i 19.21aiv ss2ab sylibr mapex
      cuni uniexg syl5eqel syl2an sylanc wb unieq syl6eqr f1oeq2 syl raleq1
      eleq2 ralbidv 3anbi123d abbidv f1oeq3 df-homeo oprabval2g 3expa mpdan )
      CLMZDLMZNZEFBOZPZWAAOZQZDMZACRZWASWCQZCMZADRZTZBUAZUBMZCDUCUDWKUEZWKEFWAU
      FZBUAZUGZWOUBMZWLVTWKWOUBUHVTWJWNUIZBUJWPVTWRBWRVTWBWFWNWIEFWAUKULUMUNWJW
      NBUOUPEUBMFUBMWQVRVSEFUBUBBUQVRCURZUBECLUSGUTVSDURZUBFDLUSHUTVAVBVRVSWLWM
      IJKCDLLIOZURZJOZURZWAPZWDXCMZAXARZWGXAMZAXCRZTZBUAWKUCEXDWAPZXFACRZWHAXCR
      ZTZBUAUBXACUEZXJXNBXOXEXKXGXLXIXMXOXBEUEXEXKVCXOXBWSEXACVDGVEXBEXDWAVFVGX
      FAXACVHXOXHWHAXCXACWGVIVJVKVLXCDUEZXNWJBXPXKWBXLWFXMWIXPXDFUEXKWBVCXPXDWT
      FXCDVDHVEXDFEWAVMVGXPXFWEACXCDWDVIVJWHAXCDVHVKVLAKBIJVNVOVPVQ $.
      $( [20-Feb-2007] $) $( [14-Feb-2007] $)
  $}

  ${ $d F f x $. $d J f x $. $d K f x $. $d X f $. $d Y f $.
    ishomeo.1 $e |- X = U. J $.
    ishomeo.2 $e |- Y = U. K $.
    $( The predicate F is a homeomorphism between topology ` J ` and
       topology ` K ` .  Based on Bourbaki TG I.2. $)
    ishomeo $p |- ( ( J e. Top /\ K e. Top /\ F e. A ) ->
      ( F e. ( J Homeo K ) <-> ( F : X -1-1-onto-> Y /\
        A. x e. J ( F " x ) e. K /\
        A. x e. K ( `' F " x ) e. J ) ) ) $=
      ( vf ctop wcel w3a chomeosm co cv wf1o cima wral ccnv cab wceq homeofval
      3adant3 eleq2d wb f1oeq1 imaeq1 eleq1d ralbidv cnveq imaeq1d 3anbi123d
      elabg 3ad2ant3 bitrd ) DKLZEKLZCBLZMZCDENOZLCFGJPZQZVBAPZRZELZADSZVBTZVDR
      ZDLZAESZMZJUAZLZFGCQZCVDRZELZADSZCTZVDRZDLZAESZMZUTVAVMCUQURVAVMUBUSAJDEF
      GHIUCUDUEUSUQVNWCUFURVLWCJCBVBCUBZVCVOVGVRVKWBFGVBCUGWDVFVQADWDVEVPEVBCVD
      UHUIUJWDVJWAAEWDVIVTDWDVHVSVDVBCUKULUIUJUMUNUOUP $.
      $( [20-Feb-2007] $) $( [14-Feb-2007] $)
  $}

  ${ $d F x $. $d J x $. $d K x $.
    hmeomap.1 $e |- X = U. J $.
    hmeomap.2 $e |- Y = U. K $.
    $( A homeomorphism is a 1-1-onto mapping. $)
    hmeomap $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> F : X -1-1-onto-> Y  ) ) $=
      ( vx ctop wcel wa chomeosm co wf1o wi w3a cv cima wral ccnv ishomeo
      biimpa 3simp1d 3exp1 imp pm2.43d ) BIJZCIJZKABCLMZJZDEANZUGUHUJUJUKOOUGUH
      UJUJUKUGUHUJPZUJKUKAHQZRCJHBSZATUMRBJHCSZULUJUKUNUOPHUIABCDEFGUAUBUCUDUEU
      F $.
      $( [5-Mar-2007] $) $( [5-Mar-2007] $)
  $}

  ${ $d F x $. $d J x $. $d K x $.
    $( The image of an open set by the converse of a homeomorphism is an open
       set. $)
    hmeocna $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. K ( `' F " x ) e. J  ) ) $=
      ( ctop wcel wa chomeosm co ccnv cv cima wral wi w3a cuni wf1o eqid
      ishomeo biimpa 3simp3d 3exp1 imp pm2.43d ) CEFZDEFZGBCDHIZFZBJAKZLCFADMZU
      EUFUHUHUJNNUEUFUHUHUJUEUFUHOZUHGCPZDPZBQZBUILDFACMZUJUKUHUNUOUJOAUGBCDULU
      MULRUMRSTUAUBUCUD $.
      $( [5-Mar-2007] $) $( [5-Mar-2007] $)
  $}

  ${ $d F x $. $d J x $. $d K x $.
    $( The image of an open set by a homeomorphism is an open set. $)
    hmeocnb $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. J ( F " x ) e. K  ) ) $=
      ( ctop wcel wa chomeosm co cv cima wral wi w3a cuni wf1o ccnv eqid
      ishomeo biimpa 3simp2d 3exp1 imp pm2.43d ) CEFZDEFZGBCDHIZFZBAJZKDFACLZUE
      UFUHUHUJMMUEUFUHUHUJUEUFUHNZUHGCOZDOZBPZUJBQUIKCFADLZUKUHUNUJUONAUGBCDULU
      MULRUMRSTUAUBUCUD $.
      $( [5-Mar-2007] $) $( [5-Mar-2007] $)
  $}

  ${ $d F x y $. $d G x y $. $d J x y $. $d K x y $. $d L x y $.
    $( The composition of two homeomorphisms is a homeomorphism. $)
    cmphmp $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
     ( ( F e. ( J Homeo K ) /\ G e. ( K Homeo L ) ) ->
     ( G o. F ) e. ( J Homeo L ) ) ) $=
      ( vx vy ctop wcel w3a chomeosm co wa ccom cuni wf1o cv cima wral ccnv wi
      eqid hmeomap 3adant3 3adant1 anim12d imp pm3.22 f1oco 3syl hmeocnb hbra1
      ax-17 hbral hban wal df-ral pm3.35 wceq imaeq2 eleq1d rcla4v syl ex
      imp3a imaco eqcomi eleq1i biimp syl6com a4s sylbi r19.21ai hmeocna cnvco
      imaeq1 ax-mp eqtr2 impcom 3jca wb cvv ishomeo 3expia 3adant2 coexg
      ancoms syl5 mpbird ) CHIZDHIZEHIZJZACDKLZIZBDEKLZIZMZBANZCEKLIZWMWRMZWTCO
      ZEOZWSPZWSFQZRZEIZFCSZWSTZXERZCIZFESZJZXAXDXHXLXAXBDOZAPZXNXCBPZMZXPXOMXD
      WMWRXQWMWOXOWQXPWJWKWOXOUAWLACDXBXNXBUBZXNUBZUCUDWKWLWQXPUAWJBDEXNXCXSXCU
      BZUCUEUFUGXOXPUHXBXNXCBAUIUJXAAXERZDIZFCSZBGQZRZEIZGDSZMZXHWMWRYHWMWOYCWQ
      YGWJWKWOYCUAWLFACDUKUDWKWLWQYGUAWJGBDEUKUEUFUGYHXGFCYCYGFYBFCULYFFGDYDDIF
      UMYFFUMUNUOYCYGXECIZXGUAZYCYIYBUAZFUPYGYJUAZYBFCUQYKYLFYKYGYJYIYKYGMBYARZ
      EIZXGYIYKYGYNYIYKYGYNUAZYIYKMYBYOYIYBURYFYNGYADYDYAUSYEYMEYDYABUTVAVBVCVD
      VEYNXGYMXFEXFYMBAXEVFVGVHVIVJVDVKVLUGVMVCXAATZYDRZCIZGDSZBTZXERZDIZFESZMZ
      XLWMWRUUDWMWOYSWQUUCWJWKWOYSUAWLGACDVNUDWKWLWQUUCUAWJFBDEVNUEUFUGUUDXKFEY
      SUUCFYSFUMUUBFEULUOUUCYSXEEIZXKUAZUUCUUEUUBUAZFUPYSUUFUAZUUBFEUQUUGUUHFUU
      GYSUUFUUEUUGYSMYPUUARZCIZXKUUEUUGYSUUJUUEUUGYSUUJUAZUUEUUGMUUBUUKUUEUUBUR
      YRUUJGUUADYDUUAUSYQUUICYDUUAYPUTVAVBVCVDVEUUJXKUUIXJCXJYPYTNZXERZUUIXIUUL
      USXJUUMUSBAVOXIUULXEVPVQYPYTXEVFVRVHVIVJVDVKVLVSVMVCVTWMWRWTXMWAZWMWSWBIZ
      UUNWRWJWLUUOUUNUAWKWJWLUUOUUNFWBWSCEXBXCXRXTWCWDWEWQWOUUOBAWPWNWFWGWHUGWI
      VD $.
      $( [11-Mar-2007] $) $( [9-Mar-2007] $)
  $}

  ${ $d J x $. $d X x $.
    idhme.1 $e |- X = U. J $.
    $( The identity function is a homeomorphism. $)
    idhme $p |- ( J e. Top -> ( I |` X ) e. ( J Homeo J ) ) $=
      ( vx ctop wcel cid cres cvv chomeosm co cuni uniexg syl5eqel resiexg syl
      w3a wf1o cv cima wral ccnv f1oi a1i wss wceq elssuni id syl6ssr resiima
      3syl eqeltrd rgen cnvresid imaeq1d eqtrd 3jca ishomeo mpbird 3expa
      anabsan mpdan ) AEFZGBHZIFZVDAAJKFZVCBIFVEVCALZIBAEMCNBIOPVCVEVFVCVCVEVFV
      CVCVEQZVFBBVDRZVDDSZTZAFZDAUAZVDUBZVJTZAFZDAUAZQVHVIVMVQVIVHBUCUDVMVHVLDA
      VJAFZVKVJAVRVJVGUEZVJBUEZVKVJUFVJAUGZVSVJVGBVSUHCUIZBVJUJZUKVRUHZULUMUDVQ
      VHVPDAVRVOVJAVRVSVOVJUFZWAVSVTWEWBVTVOVKVJVTVNVDVJVNVDUFVTBUNUDUOWCUPPPWD
      ULUMUDUQDIVDAABBCCURUSUTVAVB $.
      $( [20-Feb-2007] $) $( [14-Feb-2007] $)
  $}

  ${ $d x y f $.
    $( Formerly x and y had to be distinct but, I have removed this condition
    because it seem too strong. $)
    $( Definition of the relation ` x ` is homeomorph to ` y ` . $)
    df-hmph $a |- ~= = { <. x , y >. | ( x e. Top /\ y e. Top /\
      E. f f e. ( x Homeo y ) ) } $.
  $}

  ${ $d J f j k $. $d K f j k $.
    $( Express the predicate ` J ` is homeomorph to ` K ` . $)
    hmph $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K <->
      E. f f e. ( J Homeo K ) ) ) $=
      ( vj vk ctop wcel wa chomeo wbr cv chomeosm co wex w3a wceq eleq1 opreq1
      eleq2d exbidv 3anbi13d bicomd opreq2 eqcoms 3anbi23d df-3an syl5rbbr
      df-hmph brabg ompfl2 ) BFGZCFGZHZBCIJAKZBCLMZGZANZDKZFGZEKZFGZUNURUTLMZGZ
      ANZOUKVAUNBUTLMZGZANZOZUMUQHZDEBCFFIURBPZUSUKVDVGVAURBFQVJVCVFAVJVBVEUNUR
      BUTLRSTUAUTCPZUKULUQOVHVIVKULVAUQVGUKVKVAULUTCFQUBVKUPVFAVKUOVEUNUOVEPCUT
      CUTBLUCUDSTUEUKULUQUFUGDEAUHUIUJ $.
      $( [20-Feb-2007] $) $( [14-Feb-2007] $)
  $}

  ${ $d F x $. $d J x $. $d K x $.
    $( The converse of a homeomorphism is a homeomorphism. $)
    cnvhmpha $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> `' F e. ( K Homeo J ) ) ) $=
      ( vx chomeosm co wcel ctop wa ccnv cvv wi cnvexg w3a cuni wf1o cv cima
      wral eqid hmeomap f1ocnv syl6 imp hmeocna hmeocnb wceq wrel f1orel
      dfrel2 sylib adantr imaeq1d eleq1d ralbidva mpbird 3jca ex ancoms
      3adant3 ishomeo sylibrd 3exp impcom com3l mpcom com12 ) ABCEFZGZBHGZCHGZI
      ZAJZCBEFGZVMKGZVIVLVNLAVHMVLVOVIVNVKVJVOVIVNLZLVKVJVOVPVKVJVONVICOZBOZVMP
      ZVMDQZRBGDCSZVMJZVTRZCGZDBSZNZVNVKVJVIWFLZVOVJVKWGVLVIWFVLVIIZVSWAWEVLVIV
      SVLVIVRVQAPZVSABCVRVQVRTZVQTZUAZVRVQAUBUCUDVLVIWADABCUEUDWHWEAVTRZCGZDBSZ
      VLVIWODABCUFUDWHWDWNDBWHVTBGZIZWCWMCWQWBAVTWHWBAUGZWPVLVIWRVLVIWIWRWLWIAU
      HWRVRVQAUIAUJUKUCUDULUMUNUOUPUQURUSUTDKVMCBVQVRWKWJVAVBVCVDVEVFVG $.
      $( [5-Mar-2007] $) $( [5-Mar-2007] $)
  $}

  ${ $d F x $. $d J x $. $d K x $.
    $( The converse of a homeomorphism is a homeomorphism. $)
    cnvhmphb $p |- ( ( J e. Top /\ K e. Top /\ Rel F ) ->
      ( `' F e. ( J Homeo K ) -> F e. ( K Homeo J ) ) ) $=
      ( vx ccnv chomeosm co wcel ctop wrel w3a cvv wi cnvexg wa wceq dfrel2
      biimp eleq1d cuni wf1o cv cima wral eqid hmeomap f1ocnvb biimprd syl9
      imp31 hmeocna imaeq1d ralbidv biimpd hmeocnb adantr imp 3jca exp31
      ancoms 3adant3 wb ishomeo sylibrd 3exp1 impcom com3l syl6bi pm2.43a
      com3r 3impia mpcom com12 ) AEZBCFGZHZBIHZCIHZAJZKZACBFGHZVNEZLHZVPVTWAMVN
      VONVTWCVPWAVQVRVSWCVPWAMZMVSWCVQVROZWDWCVSWEWDMZVSWCALHZVSWFMVSWBALVSWBAP
      AQRZSWEWGVSWDVRVQWGVSWDMMVRVQWGVSWDVRVQWGKZVSOVPCTZBTZAUAZADUBZUCZBHZDCUD
      ZVNWMUCCHDBUDZKZWAWIVSVPWRMZVRVQVSWSMZWGVQVRWTWEVSVPWRWEVSOZVPOWLWPWQWEVS
      VPWLWEVPWKWJVNUAZVSWLVNBCWKWJWKUEZWJUEZUFVSWLXBWJWKAUGUHUIUJWEVSVPWPWEVPW
      BWMUCZBHZDCUDZVSWPDVNBCUKVSXGWPVSXFWODCVSXEWNBVSWBAWMWHULSUMUNUIUJXAVPWQW
      EVPWQMVSDVNBCUOUPUQURUSUTVAUQWIWAWRVBVSDLACBWJWKXDXCVCUPVDVEVFVGVHVIVJVKV
      GVLVM $.
      $( [11-Mar-2007] $) $( [5-Mar-2007] $)
  $}

  ${ $d F j $. $d J j $. $d K j $.
    $( The converse of a homeomorphism is a homeomorphism. $)
    cnvhmph $p |- ( ( ( J e. Top /\ K e. Top /\ F e. A ) /\ Rel F ) ->
      ( F e. ( J Homeo K ) <-> `' F e. ( K Homeo J ) ) ) $=
      ( vj ctop wcel w3a wrel wa cuni ccnv wf1o cv cima wral chomeosm co wb
      f1ocnvb bicomd adantl wceq dfrel2 biimp imaeq1d eleq1d ralbidv 3anbi12d
      3ancomb syl5bb cvv id cnvexg 3anim123i 3com12 adantr eqid ishomeo syl
      3bitr4rd ) CFGZDFGZBAGZHZBIZJZDKZCKZBLZMZVJENZOCGEDPZVJLZVLOZDGZECPZHZVIV
      HBMZBVLOZDGZECPZVMHZVJDCQRGZBCDQRGZVGVKVQVMHWCVRVGVKVSVQWBVMVFVKVSSVEVFVS
      VKVIVHBTUAUBVFVQWBSVEVFVPWAECVFVOVTDVFVNBVLVFVNBUCBUDUEUFUGUHUBUIVKVMVQUJ
      UKVGVCVBVJULGZHZWDVRSVEWGVFVCVBVDWGVCVCVBVBVDWFVCUMVBUMBAUNUOUPUQEULVJDCV
      HVIVHURZVIURZUSUTVEWEWCSVFEABCDVIVHWIWHUSUQVA $.
      $( [5-Mar-2007] $) $( [15-Feb-2007] $)
  $}

  ${ $d J f g $. $d J f x $. $d K f g $. $d K f x $.
    $( "Is homeomorph to" is symmetric. $)
    hmphsyma $p |- ( ( J e. Top /\ K e. Top ) ->
        ( J ~= K -> K ~= J ) ) $=
      ( vf vg vx ctop wcel wa cv chomeosm co wex chomeo wbr cvv wi visset w3a
      cuni wf1o cima wral ccnv eqid ishomeo eleq1 cla4egv cnvexg 3ad2ant3
      adantr wrel f1orel 3ad2ant1 adantl f1ocnv a1i wceq dfrel2 biimp eqcomd
      imaeq1d eleq1d biimpd ad2antrr r19.20dva idd 3anim123d ex imp3a mpcom
      3ancomb sylib wb syl3an3 3com12 mpbird sylc sylbid mp3an3 19.23adv hmph
      ancoms 3imtr4d ) AFGZBFGZHZCIZABJKGZCLDIZBAJKZGZDLZABMNBAMNZWFWHWLCWDWEWG
      OGZWHWLPCQWDWEWNRZWHASZBSZWGTZWGEIZUAZBGZEAUBZWGUCZWSUAAGEBUBZRZWLEOWGABW
      PWQWPUDZWQUDZUEWOXEWLXCOGZXCWJGZWLWOXEHZWKXIDXCOWIXCWJUFUGWOXHXEWNWDXHWEW
      GOUHZUIUJXJXIWQWPXCTZXDXCUCZWSUAZBGZEAUBZRZXJXLXPXDRZXQWGUKZXJXRXEXSWOWRX
      BXSXDWPWQWGULUMUNXSWOXEXRXSWOXEXRPXSWOHZWRXLXBXPXDXDWRXLPXTWPWQWGUOUPXTXA
      XOEAXSXAXOPWOWSAGXSXAXOXSWTXNBXSWGXMWSXSXMWGXSXMWGUQWGURUSUTVAVBVCVDVEXTX
      DVFVGVHVIVJXLXPXDVKVLWOXIXQVMZXEWEWDWNYAWEWDXHYAWNEOXCBAWQWPXGXFUEXKVNVOU
      JVPVQVHVRVSVTCABWAWEWDWMWLVMDBAWAWBWC $.
      $( [11-Mar-2007] $) $( [8-Mar-2007] $)
  $}


  ${
    $( "Is homeomorph to" is symmetric. $)
    hmphsym $p |- ( ( J e. Top /\ K e. Top ) ->
        ( J ~= K <-> K ~= J ) ) $=
      ( ctop wcel wa chomeo wbr hmphsyma wi ancoms impbid ) ACDZBCDZEABFGZBAFGZ
      ABHMLONIBAHJK $.
      $( [11-Mar-2007] $) $( [22-Feb-2007] $)
  $}

  ${ $d J f $. $d J x $.
    $( "Is homeomorph to" is reflexive. $)
    hmphre $p |- ( J e. Top -> J ~= J ) $=
      ( vf vx ctop wcel chomeo wbr wa cv chomeosm co wex cuni cvv wi uniexg
      cid cres resiexg w3a wf1o cima wral ccnv f1oi a1i wss elssuni resiima
      eleq1d biimprcd mpd rgen wceq adantl pm3.26 eqeltrd cnvresid imaeq1
      ax-mp syl5eqel mpdan 3jca eqid ishomeo mpbird 3exp pm2.43i com12 eleq1
      imbi2d cla4egv 19.37v sylib syl imp hmph anidms ) ADEZAAFGZVSVSHVTBIZAAJK
      ZEZBLZVSVSWDVSAMZNEZVSWDOZADPWFQWERZNEZWGWENSWIVSWCOZBLZWGWIVSWHWBEZOZWKV
      SWIWLVSWIWLOVSVSWIWLVSVSWITZWLWEWEWHUAZWHCIZUBZAEZCAUCZWHUDZWPUBZAEZCAUCZ
      TWNWOWSXCWOWNWEUEUFWSWNWRCAWPAEZWPWEUGZWRWPAUHZXEWRXDXEWQWPAWEWPUIZUJUKUL
      UMUFXCWNXBCAXDXEXBXFXDXEHZWQAXAXHWQWPAXEWQWPUNXDXGUOXDXEUPUQWTWHUNXAWQUNW
      EURWTWHWPUSUTVAVBUMUFVCCNWHAAWEWEWEVDZXIVEVFVGVHVIWJWMBWHNWAWHUNWCWLVSWAW
      HWBVJVKVLULVSWCBVMVNVOVOVPBAAVQVFVR $.
      $( [5-Mar-2007] $) $( [25-Feb-2007] $)
  $}

  ${ $d J f g h $. $d K f g $. $d L f g h $.
    $( "Is homeomorph to" is transitive. $)
    hmphtr $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( J ~= K /\ K ~= L ) -> J ~= L ) ) $=
      ( vf vg vh ctop wcel w3a cv chomeosm co wex chomeo wbr wa wi ccom cvv
      eleq1 cla4egv coexg adantl cmphmp ancomsd imp sylc exp32 19.23adv com23
      imp3a wb hmph 3adant2 sylibrd biimpd 3adant3 3adant1 syl2and ) AGHZBGHZCG
      HZIZDJZABKLZHZDMZEJZBCKLZHZEMZACNOZABNOZBCNOZVCVGVKPFJZACKLZHZFMZVLVCVGVK
      VRVCVFVKVRQDVCVKVFVRVCVJVFVRQEVCVJVFVRVHVDRZSHZVSVPHZVRVCVJVFPZPVQWAFVSSV
      OVSVPTUAWBVTVCVHVDVIVEUBUCVCWBWAVCVFVJWAVDVHABCUDUEUFUGUHUIUJUIUKUTVBVLVR
      ULVAFACUMUNUOUTVAVMVGQVBUTVAPVMVGDABUMUPUQVAVBVNVKQUTVAVBPVNVKEBCUMUPURUS
      $.
      $( [24-Mar-2007] $) $( [9-Mar-2007] $)
  $}

  ${ $d x y z $.
    $( ` ~= ` is a relation whose domain is included in Top. $)
    dmhmph $p |- dom ~= (_ Top $=
      ( vx vy vz chomeo cdm ctop cxp wss cv wcel wa chomeosm co wex copab w3a
      df-hmph df-3an opabbii eqtr opabssxp eqsstr dmss ax-mp dmxpid sseqtr ) DE
      ZFFGZEZFDUHHUGUIHDAIZFJZBIZFJZKCIUJULLMJCNZKZABOZUHDUKUMUNPZABOUPABCQUQUO
      ABUKUMUNRSTUNABFFUAUBDUHUCUDFUEUF $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)

    $( ` ~= ` is a relation whose range is included in Top. $)
    rnhmph $p |- ran ~= (_ Top $=
      ( vx vy vz chomeo crn ctop cxp wss cv wcel wa chomeosm co wex copab w3a
      df-hmph df-3an opabbii eqtr opabssxp eqsstr rnss ax-mp rnxpss sstri ) DEZ
      FFGZEZFDUHHUGUIHDAIZFJZBIZFJZKCIUJULLMJCNZKZABOZUHDUKUMUNPZABOUPABCQUQUOA
      BUKUMUNRSTUNABFFUAUBDUHUCUDFFUEUF $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)
  $}

  ${
    dmhmpha.1 $e |- A e. V $.
    $( A simple consequence of ~ dmhmph . A sort of lemma for ~ hmpher since it
       is used several times by it. $)
    dmhmpha $p |- ( A ~= B -> A e. Top ) $=
      ( chomeo wbr cdm wcel ctop breldm dmhmph sseli syl ) ABDEADFZGAHGABDCIMHA
      JKL $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)
  $}

  ${
    rnhmpha.1 $e |- A e. V $.
    rnhmpha.2 $e |- B e. V $.
    $( A simple consequence of ~ rnhmph .  A sort of lemma for ~ hmpher since
       it is used several times by it. $)
    rnhmpha $p |- ( A ~= B -> B e. Top ) $=
      ( chomeo wbr crn wcel ctop brelrn rnhmph sseli syl ) ABEFBEGZHBIHABECDJNI
      BKLM $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)
  $}


  ${ $d x y z $.
    $( "Is homeomorph" to is an equivalence relation. $)
    hmpher $p |- Er ~= $=
      ( vx vy vz chomeo cv ctop wcel wa wbr visset dmhmpha rnhmpha jca
      hmphsyma mpcom w3a adantr adantl 3jca hmphtr ster ) ABCDAEZFGZBEZFGZHUBUD
      DIZUDUBDIUFUCUEUBUDAJZKZUBUDUGBJZLMUBUDNOUCUECEZFGZPUFUDUJDIZHZUBUJDIUMUC
      UEUKUFUCULUHQULUEUFUDUJUIKRULUKUFUDUJUICJLRSUBUDUJTOUA $.
      $( [24-Mar-2007] $) $( [22-Mar-2007] $)
  $}

  ${
    hmphsymv.1 $e |- A e. V $.
    hmphsymv.2 $e |- B e. V $.
    $( A more general version of ~ hmphsym .  Certainly not a useful proof
       (since it's a simple consequence of ~ hmpher ) but it shows that the
       conditions of ~ hmphsym could (and should) be weakened. $)
    hmphsymv $p |- ( A ~= B <-> B ~= A ) $=
      ( chomeo hmpher ersymb ) ABECDFG $.
      $( [24-Mar-2007] $) $( [23-Mar-2007] $)

  $}

  ${ $d J a b c j x y z $. $d f t u v w $. $d v w x y z $.
  $( Homeomorphisms on a topology ` J ` is a group for composition. This
  means from Felix Klein's point of view that a set equipped with a
  topology is a geometry, namely the  rubber sheet geometry. $)
  hmeogrp $p |- ( J e. Top -> { <. <. x , y >. , z >. |  ( x e. ( J Homeo J )
    /\ y e. ( J Homeo J )  /\ z = ( x o. y ) ) } e. Grp ) $=
    ( va vb vc vj vv vw vt vf vu ctop wcel cv chomeosm co ccom wceq w3a copab2
    cgr cxp wf wral wrex wa wi cmphmp ancomsd 3exp pm2.43d pm2.43i r19.21aivv
    df-3an oprabbii foprab2 sylib coass cvv coeq1 coeq2 oprabval2g opreq1d
    3simp1 adantl 3simp2 coexg 3adant3 syl3anc com12 ancoms impcom 3simp3
    sylanc eqtrd 3adant1 opreq2d 3eqtr4a 3exp2 imp32 r19.21aiv ex cid cuni
    cres opreq1 eqeq1d eqeq2 rexbidv anbi12d ralbidv rcla4ev eqid idhme adantr
    pm3.27 exp3a mpid imp wf1o hmeomap anidms f1of fcoi2 3syl ccnv cnvhmpha
    mpd f1ococnv1 syl jca r19.21aiva 3jca wb oprex pm3.2i a1i oprabex2g wex
    cab crn eleq1a syl6 com3l expcom 3imp 19.23aivv eqeq2d anbi2d fcoi1 eqcomd
    3ancoma 3anass bitr exbii df-rex bitr4 sylibr eleq1 bicomd eqeq1 3anbi3d
    exbidv imbi12d mpbiri a4w impbid abbi1dv wn pm3.26 con3i c0 cima wfun wf1
    f1o5 df-f1 anbi1i anass 3bitr abbii visset uniex fabex eqeltr df-homeo
    dmoprab2 ndmoprgOLD eleq2 noel pm2.21i syl6bi syl5 19.23advv impbid1 eqtr4d
    cdm wrel reldmoprab dmeqi releqi mpbir oprprc1 a1d pm2.61i rnoprab eqtr4
    isgrp mpbird ) DNOZAPZDDQRZOZBPZUWNOZCPZUWMUWPSZTZUAZABCUBZUCOZUWNUWNUDUWNU
    XBUEZEPZFPZUXBRZGPZUXBRZUXEUXFUXHUXBRZUXBRZTZGUWNUFZFUWNUFEUWNUFZHPZUXEUXBR
    ZUXETZUXFUXEUXBRZUXOTZFUWNUGZUHZEUWNUFZHUWNUGZUAZUWLUXDUXNUYCUWLUWSUWNOZBUW
    NUFAUWNUFUXDUWLUYEABUWNUWNUWLUWOUWQUHZUYEUIZUWLUWLUYGUWLUWLUWLUYGUWLUWLUWLU
    AZUWQUWOUYEUWPUWMDDDUJZUKULUMUNUOABCUWNUWNUWSUWNUXBUXAUYFUWTUHABCUWOUWQUWTU
    PUQZURUSUWLUXMEFUWNUWNUWLUXEUWNOZUXFUWNOZUHZUXMUWLUYMUHUXLGUWNUWLUYKUYLUXHU
    WNOZUXLUIUWLUYKUYLUYNUXLUWLUYKUYLUYNUAZUHZUXEUXFSZUXHSZUXEUXFUXHSZSZUXIUXKU
    XEUXFUXHUTUYPUXIUYQUXHUXBRZUYRUYKUYLUYQVAOZUXIVUATUYPUYKUYLVUBUAUXGUYQUXHUX
    BABCUXEUXFUWNUWNUWSUYQUXBUXEUWPSZVAUWMUXEUWPVBZUWPUXFUXEVCUYJVDVEUYOUYKUWLU
    YKUYLUYNVFVGZUYOUYLUWLUYKUYLUYNVHVGZUYOVUBUWLUYKUYLVUBUYNUXEUXFUWNUWNVIVJZV
    GVKUYQUWNOZUYNUYRVAOZVUAUYRTUYPABCUYQUXHUWNUWNUWSUYRUXBUYQUWPSVAUWMUYQUWPVB
    UWPUXHUYQVCUYJVDUYOUWLVUHUYKUYLUWLVUHUIZUYNUYLUYKVUJUWLUYLUYKUHZVUHUWLVUKVU
    HUIZUWLUWLVULUWLUWLUWLVULUXFUXEDDDUJULUMUNVLVMVJVNUYOUYNUWLUYKUYLUYNVOZVGZU
    YOVUIUWLVUBUYNVUIUYOUYQUXHVAUWNVIVUGVUMVPVGVKVQUYPUXKUXEUYSUXBRZUYTUYPUXJUY
    SUXEUXBUYLUYNUYSUWNOZUXJUYSTUYPABCUXFUXHUWNUWNUWSUYSUXBUXFUWPSUWNUWMUXFUWPV
    BUWPUXHUXFVCUYJVDVUFVUNUYOUWLVUPUYLUYNUWLVUPUIZUYKUYNUYLVUQUWLUYNUYLUHZVUPU
    WLVURVUPUIZUWLUWLVUSUWLUWLUWLVUSUXHUXFDDDUJULUMUNVLVMVRVNZVKVSUYKVUPUYTVAOZ
    VUOUYTTUYPABCUXEUYSUWNUWNUWSUYTUXBVUCVAVUDUWPUYSUXEVCUYJVDVUEVUTUYKUYSVAOZV
    VAUYPUXEUYSUWNVAVIVUEUYOVVBUWLUYLUYNVVBUYKUXFUXHUWNUWNVIVRVGVPVKVQVTWAWBWCW
    DUOWEDWFZWGZUWNOZVVDUXEUXBRZUXETZUXRVVDTZFUWNUGZUHZEUWNUFZUYCUWLUYBVVKHVVDU
    WNUXOVVDTZUYAVVJEUWNVVLUXQVVGUXTVVIVVLUXPVVFUXEUXOVVDUXEUXBWHWIVVLUXSVVHFUW
    NUXOVVDUXRWJWKWLWMWNDVVCVVCWOZWPZUWLVVJEUWNUWLUYKUHZVVGVVIVVOVVFVVDUXESZUXE
    VVEUYKVVPUWNOZVVFVVPTVVOABCVVDUXEUWNUWNUWSVVPUXBVVDUWPSUWNUWMVVDUWPVBUWPUXE
    VVDVCUYJVDUWLVVEUYKVVNWQUWLUYKWRZUWLUYKVVQUWLUYKVVEVVQVVNUWLUYKVVEVVQUIUIZU
    WLUWLVVSUWLUWLUWLVVSUYHUYKVVEVVQUXEVVDDDDUJWSULUMUNWTXAVKVVOVVCVVCUXEXBZVVC
    VVCUXEUEVVPUXETUWLUYKVVTUWLUYKVVTUIUXEDDVVCVVCVVMVVMXCXDXAZVVCVVCUXEXEVVCVV
    CUXEXFXGVQUXEXHZUWNOZVWBUXEUXBRZVVDTZVVIVVOVVHVWEFVWBUWNUXFVWBTUXRVWDVVDUXF
    VWBUXEUXBWHWIWNUWLUYKVWCUWLUYKVWCUIUXEDDXIXDXAZVVOVWDVWBUXESZVVDVWCUYKVWGUW
    NOZVWDVWGTVVOABCVWBUXEUWNUWNUWSVWGUXBVWBUWPSUWNUWMVWBUWPVBUWPUXEVWBVCUYJVDV
    WFVVRVVOVWCVWHVWFUWLUYKVWCVWHUIUWLUYKVWCVWHUWLUYKVWCUHVWHUIZUWLUWLVWIUWLUWL
    UWLVWIUXEVWBDDDUJULUMUNWSXAXJVKVVOVVTVWGVVDTVWAVVCVVCUXEXKXLVQVPXMXNVPXOUWL
    UWNVAOZVWJUHZUXBVAOUXCUYDXPVWKUWLVWJVWJDDQXQZVWLXRXSABCUWNUWNUWSVAVAUXBUYJX
    TEFGHVAUXBUWNUWNUXABYAZAYAZCYBZUXBYCUWLUWNVWOTZUWLVWOUWNUWLVWNCUWNUWLVWNUWR
    UWNOZVWNUWLVWQUXAUWLVWQUIZABUWOUWQUWTVWRUWQUWOUWTVWRUIUWLUWQUWOUHZUWTVWQUWL
    VWSUWTVWQUIZUIZUWLUWLVXAUWLUWLUWLVXAUYHVWSUYEVWTUYIUWSUWNUWRYDYEULUMUNYFYGY
    HYIVLUWLVWQVWNUWLVWQUHZVWMACUWMUWRTZVXBVWMUIUWLUWOUHZUWOUWQUWMUWSTZUAZBYAZU
    IVXDUWOVXEUHZBUWNUGZVXGVVEUWOUWMUWMVVDSZTZUHZVXIVXDVXHVXLBVVDUWNUWPVVDTZVXE
    VXKUWOVXMUWSVXJUWMUWPVVDUWMVCYJYKWNUWLVVEUWOVVNWQVXDUWOVXKUWLUWOWRUWLUWOVXK
    UWLUWOVXKUIUWLUWLUHZUWOVVCVVCUWMXBZVXKUWMDDVVCVVCVVMVVMXCVXOVXJUWMVXOVVCVVC
    UWMUEVXJUWMTVVCVVCUWMXEVVCVVCUWMYLXLYMYEXDXAXMVPVXGUWQVXHUHZBYAVXIVXFVXPBVX
    FUWQUWOVXEUAVXPUWOUWQVXEYNUWQUWOVXEYOYPYQVXHBUWNYRYSYTVXCVXBVXDVWMVXGVXCVWQ
    UWOUWLVXCUWOVWQUWMUWRUWNUUAUUBYKVXCUXAVXFBVXCUWTVXEUWOUWQVXCVXEUWTUWMUWRUWS
    UUCUUBUUDUUEUUFUUGUUHWDUUIUUJYMUWLUUKVXNUUKZVWPVXNUWLUWLUWLUULUUMDVAOZVXQVW
    PUIVXRVXQVWPVXRVXQUHZUWNUUNVWODDVANQABCNNUWMWFZUWPWFZIPZXBZVYBJPZUUOUWPOJUW
    MUFZVYBXHZVYDUUOUWMOJUWPUFZUAZIYBZQVYIVXTVYAVYBUEZVYFUUPZVYBYCVYATZUHZUHZVY
    EVYGUHZUHZIYBZVAVYHVYPIVYHVYCVYOUHVYPVYCVYEVYGYOVYCVYNVYOVYCVXTVYAVYBUUQZVY
    LUHVYJVYKUHZVYLUHVYNVXTVYAVYBUURVYRVYSVYLVXTVYAVYBUUSUUTVYJVYKVYLUVAUVBUUTY
    PUVCVYMVYOUHZIVXTVYAVYQUWMAUVDUVEUWPBUVDUVEVYPVYJVYTUHIVYJVYMVYOUVAUVCUVFUV
    GJCIABUVHUVIUVJZVXSVWNCUUNVXSVWNUWRUUNOZVXSUWNUUNTZVWNWUBUIZWUAWUCUXAWUBABW
    UCUWOWUBUXAWUCUWOUWMUUNOZWUBUWNUUNUWMUVKWUEWUBUWMUVLUVMUVNUWOUWQUWTVFUVOUVP
    ZXLWUBVWNUWRUVLUVMZUVQUUJUVRWDVXRUUKZVWPVXQWUHUWNUUNVWODDQQUVSZUVTKPZNOLPZN
    OUHVYBWUJWFWUKWFMPZXBWULVYDUUOWUKOJWUJUFWULXHVYDUUOWUJOJWUKUFUAMYBTUHZKLIUB
    ZUVSZUVTWUMKLIUWAWUIWUOQWUNJIMKLUVHUWBUWCUWDUWEZWUHVWNCUUNWUHVWNWUBWUHWUCWU
    DWUPWUFXLWUGUVQUUJUVRUWFUWGXLUWGUXAABCUWHUWIUWJXGUWK $.
    $( [4-Feb-2008] $) $( [3-Feb-2008] $)
  $}


