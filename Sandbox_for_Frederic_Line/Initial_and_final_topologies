$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Initial and final topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c subSp $. $( Function returning a subspace topology. $)

  $( Extend class notation with the function returning a subspace topology. $)
  csubsp $a class subSp $.

  ${
    $d x y z u $. $d x y z v $.
    $( Function returning the subspace topology induced by the topology
     ` y ` and the set ` x ` . $)
    df-subsp $a |- subSp = { <. <. x , y >. , z >. |
       ( y e. Top /\ z = { u | E. v e. y u = ( v i^i x ) } ) } $.
  $}

  ${
    $d A u v x y z $. $d J u v x y z $.
    subsp.1 $e |- J e. Top $.
    subsp.2 $e |- A e. V $.
    $( The subspace topology induced by the topology ` J ` and the set ` A ` .
       To Norm: there is no reason to consider that ` J e. Top ` and ` A e. V `
       should be premises.  I will transform them into antecedents as soon as
       possible. $)
    subsp $p |- ( subSp ` <. A , J >. ) = { u | E. v e. J u = ( v i^i A ) }
      $=
      ( vx vy vz csubsp co cv cvv wcel ctop wa cin wceq wrex cab copab2 cop
      cfv df-subsp wb visset ibar anbi1d ax-mp oprabbii eqtr opreq df-opr wss
      id inss2 a1i eqsstrd pm4.71ri rexbii r19.42v bitr abbii abssexg eqeltr
      ineq2 eqeq2d rexbidv abbidv rexeq1 eqid oprabval2 mp2an 3eqtr3 ) CDJKZCDG
      LZMNZHLZONZPZILBLZALZVPQZRZAVRSZBTZRZPZGHIUAZKZCDUBJUCWAWBCQZRZADSZBTZJWI
      RVOWJRJVSWGPZGHIUAWIGHIABUDWOWHGHIVQWOWHUEGUFVQVSVTWGVQVSUGUHUIUJUKCDJWIU
      LUICDJUMCMNZDONWJWNRFEGHICDMOWFWNWIWLAVRSZBTWNWACUNZWMPZBTZMWMWSBWMWRWLPZ
      ADSWSWLXAADWLWRWLWAWKCWLUOWKCUNWLWBCUPUQURUSUTWRWLADVAVBVCWPWTMNFWMBCMVDU
      IVEVPCRZWEWQBXBWDWLAVRXBWCWKWAVPCWBVFVGVHVIVRDRWQWMBWLAVRDVJVIWIVKVLVMVN
      $.
      $( [4-Jun-2007] $) $( [29-May-2007] $)
  $}

  ${
    $d X x y z $. $d J x y z $.  $d R x y z $.
    qusp.1 $e |- X = U. J $.
    qusp.2 $e |- Er R $.
    $( A quotient space is a topology. $)
    qusp $p |- ( J e. Top ->
      { x | ( x (_ ( X /. R ) /\ U. x e. J ) } e. Top ) $=
      ( vy vz ctop wcel cv cqs wss cuni wa cab wi wal cin wral simplr wceq wex
      uniopnt simpll df-ral hba1 ax-17 eleq1a imim2i com23 imp3a a4s 19.23ad
      19.21aiv sylbi adantl abss sylibr sylanc uniuni syl5eqel jca exp31
      unissb syl5ibr r19.26 syl5ib ssab visset uniex sseq1 unieq eleq1d
      anbi12d elab 3imtr4g ssinss1 ad2antrr uninqs ad2antrl inopnt 3expib
      com12 impcom eqeltrd ex an4 exp3a imp elabf inex1 r19.21aiv cvv wb cpw
      elpw bicomi a1i anbi1d abbidv ssab2 uniexg qsexg pwexg 3syl ssexg syldan
      mpan istopg syl mpbird ) CIJZAKZDBLZMZXNNZCJZOZAPZIJZGKZXTMZYBNZXTJZQZGRZ
      YBHKZSZXTJZHXTTZGXTTZOZXMYGYLXMYFGXMXNYBJZXSQARZYDXOMZYDNZCJZOZYCYEXMXSAY
      BTZYSYOXMXPAYBTZXRAYBTZOYSYTXMUUAUUBYSXMYPUUBYSQUUAXMYPUUBYSXMYPOZUUBOZYP
      YRXMYPUUBUAUUDYHXQUBZYNOZAUCZHPZNZCYQXMUUHCMZUUICJUUDUUHCUDXMYPUUBUEUUDUU
      GYHCJZQZHRZUUJUUBUUMUUCUUBYNXRQZARZUUMXRAYBUFUUOUULHUUOUUFUUKAUUNAUGUUKAU
      HUUNUUFUUKQAUUNUUEYNUUKUUNYNUUEUUKXRUUEUUKQYNXQCYHUIUJUKULUMUNUOUPUQUUGHC
      URUSUTHAYBVAVBVCVDAYBXOVEVFULXPXRAYBVGVHXSAYBUFVFXSAYBVIXSYSAYDYBGVJZVKXN
      YDUBZXPYPXRYRXNYDXOVLUUQXQYQCXNYDVMVNVOVPVQUOXMYBXTJZYKQZGRYLXMUUSGXMYBXO
      MZYDCJZOZYKUURXMUVBYKXMUVBOZYJHXTUVCYHXOMZYHNZCJZOZYIXOMZYINZCJZOZYHXTJYJ
      XMUVBUVGUVKQXMUVBUVGUVKXMUUTUVDOZUVAUVFOZOZUVKUVBUVGOXMUVNUVKXMUVNOZUVHUV
      JUVNUVHXMUUTUVHUVDUVMYBYHXOVRVSUQUVOUVIYDUVESZCUVLUVIUVPUBXMUVMDYBYHBFVTW
      AUVNXMUVPCJZUVMXMUVQQUVLXMUVMUVQXMUVAUVFUVQYDUVECWBWCWDUQWEWFVCWGUUTUVAUV
      DUVFWHVHWIWJXSUVGAYHUVGAUHHVJXNYHUBZXPUVDXRUVFXNYHXOVLUVRXQUVECXNYHVMVNVO
      WKXSUVKAYIUVKAUHYBYHUUPWLXNYIUBZXPUVHXRUVJXNYIXOVLUVSXQUVICXNYIVMVNVOWKVQ
      WMWGXSUVBAYBUVBAUHUUPXNYBUBZXPUUTXRUVAXNYBXOVLUVTXQYDCXNYBVMVNVOWKVHUOYKG
      XTUFUSVCXMXTWNJYAYMWOXMXTXNXOWPZJZXROZAPZWNXMXSUWCAXMXPUWBXRXPUWBWOXMUWBX
      PXNXOAVJWQWRWSWTXAUWDUWAMZXMUWDWNJZXRAUWAXBUWEXMUWAWNJZUWFUWEXMODWNJZXOWN
      JUWGXMUWHUWEXMCNWNDCIXCEVBUQDBXDXOWNXEXFUWDUWAWNXGXHXIWFGHWNXTXJXKXL $.
      $( [4-Jun-2007] $) $( [4-Jun-2007] $)
  $}

