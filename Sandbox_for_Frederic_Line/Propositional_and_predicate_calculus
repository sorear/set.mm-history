$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Propositional and predicate calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ahypfmbi.1 $e |- ( ps <-> ch ) $.
    $( Add a hypothesis into the first member of a biimplication. $)
    ahypfmbi $p |- ( ph -> ( ( ph /\ ps ) <-> ch ) ) $=
      ( wa anbi2i baib ) ABEACBCADFG $.
      $( [20-Feb-2007] $) $( [30-Jan-2007] $)
  $}

  ${
    ompfl2.1 $e |- ( ph -> ( ps <-> ( ph /\ ch ) ) ) $.
    $( Remove a hypothesis from the second member of a biimplication. $)
    ompfl2 $p |- ( ph -> ( ps <-> ch ) ) $=
      ( wi wa biimpd pm5.42 mpbir biimprd exp3a pm2.43i impbid ) ABCABCEEABACFZ
      EEABNDGABCHIACBEAACBABNDJKLM $.
      $( [20-Feb-2007] $) $( [15-Feb-2007] $)
  $}

  ${
    eqexst.1 $e |- ( ph -> A. x ph ) $.
    $( We can remove the existential operator when the variable doesn't
       belong to the statement. $)
    eqexst $p |- ( E. x ph <-> ph ) $=
      ( wex wal wi 19.9t mpg 19.8a impbi ) ABDZAAABEFKAFBABGCHABIJ $.
      $( [24-Mar-2007] $) $( [24-Mar-2007] $)
  $}

  ${
    $d x y $. $d ph x $. $d ph y $. $d ch x $. $d ps y $.
    $d A x $. $d B x y $.
    rexxfrd.1 $e |- ( ( ph /\ y e. B ) -> A e. B ) $.
    rexxfrd.2 $e |- ( ( ph /\ x e. B ) -> E. y e. B x = A ) $.
    rexxfrd.3 $e |- ( ( ph /\ x = A ) -> ( ps <-> ch ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` .  (Part of FL's "sandbox",
       15-Apr-2007.) $)
    rexxfrd $p |- ( ph -> ( E. x e. B ps <-> E. y e. B ch ) ) $=
      ( wn wral wrex cv wceq wa negbid ralxfrd dfrex2 3bitr4g ) ABKZDGLZKCKZEGL
      ZKBDGMCEGMAUBUDAUAUCDEFGHIADNFOPBCJQRQBDGSCEGST $.
      $( [15-Apr-2007] $) $( [10-Apr-2007] $)
  $}


