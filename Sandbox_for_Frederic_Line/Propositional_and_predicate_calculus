$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Propositional and predicate calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ahypfmbi.1 $e |- ( ps <-> ch ) $.
    $( Add a hypothesis into the first member of a biimplication. $)
    ahypfmbi $p |- ( ph -> ( ( ph /\ ps ) <-> ch ) ) $=
      ( wa anbi2i baib ) ABEACBCADFG $.
      $( [20-Feb-2007] $) $( [30-Jan-2007] $)
  $}

  ${
    ompfl2.1 $e |- ( ph -> ( ps <-> ( ph /\ ch ) ) ) $.
    $( Remove a hypothesis from the second member of a biimplication. $)
    ompfl2 $p |- ( ph -> ( ps <-> ch ) ) $=
      ( wi wa biimpd pm5.42 mpbir biimprd exp3a pm2.43i impbid ) ABCABCEEABACFZ
      EEABNDGABCHIACBEAACBABNDJKLM $.
      $( [20-Feb-2007] $) $( [15-Feb-2007] $)
  $}

  ${
    intn3and.1 $e |- ( ph -> -. ps ) $.

    $( Introduction of a conjunct inside a contradiction. $)
    intn3an1d $p |- ( ph -> -. ( ps /\ ch /\ th ) ) $=
      ( wa wn w3a intnanrd df-3an negbii sylibr ) ABCFZDFZGBCDHZGAMDABCEIIONBCD
      JKL $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)

    $( Introduction of a conjunct inside a contradiction. $)
    intn3an2d $p |- ( ph -> -. ( ch /\ ps /\ th ) ) $=
      ( wa wn w3a intnand intnanrd df-3an negbii sylibr ) ACBFZDFZGCBDHZGANDABC
      EIJPOCBDKLM $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)

    $( Introduction of a conjunct inside a contradiction. $)
    intn3an3d $p |- ( ph -> -. ( ch /\ th /\ ps ) ) $=
      ( wa wn w3a intnand df-3an negbii sylibr ) ACDFZBFZGCDBHZGABMEIONCDBJKL
      $.
      $( [3-Jan-2008] $) $( [27-Dec-2007] $)
  $}


  ${
    $d A x $. $d B x $. $d ph x $. $d ps x $.
    rcla4dev.1 $e |- ( ( ph /\ x = A ) -> ( ps <-> ch ) ) $.
    $( Restricted existential specialization with implicit substitution. $)
    rcla4dev $p |- ( ( ph /\ A e. B ) -> ( ps -> E. x e. B ch ) ) $=
      ( wcel wrex wi wa cv wceq wb bicomd expcom pm5.74d rcla4ev r19.37av syl
      ex pm2.86d impcom ) EFHZABCDFIZJUDABUEUDABJZAUEJZUDUFKACJZDFIUGUHUFDEFDLE
      MZACBAUICBNAUIKBCGOPQRACDFSTUAUBUC $.
      $( [19-May-2007] $) $( [17-Apr-2007] $)
  $}


  $( ` /\ ` associativity. $)
  and4as $p |- ( ( ph /\ ps /\ ( ch /\ th ) ) <->
    ( ( ph /\ ps /\ ch ) /\ th ) ) $=
    ( wa w3a wi pm3.2 3exp imp4a 3imp id exp4a 3imp1 impbi ) ABCDEZFZABCFZDEZAB
    PSABCDSABCDSGRDHIJKABCDQABCDQABPQQLIMNO $.
    $( [14-Jul-2007] $) $( [14-Jul-2007] $)

  $( ` /\ ` associativity. $)
  and4com $p |- ( ( ph /\ ( ps /\ ch /\ th ) ) <->
    ( ( ph /\ ps /\ ch ) /\ th ) ) $=
    ( w3a wa wi pm3.2 3exp 3imp2 3expd 3imp1 impbi ) ABCDEZFZABCEZDFZABCDQABCDQ
    GPDHIJABCDOABCDOANHKLM $.
    $( [14-Jul-2007] $) $( [14-Jul-2007] $)

  $( Conjunct idempotence deduction equivalency. (Contributed by Roy F.
     Longton, 8-Aug-2005.) $)
  anidmdbi $p |- ( ( ph -> ( ps /\ ps ) ) <-> ( ph -> ps ) ) $=
    ( wa anidm imbi2i ) BBCBABDE $.
    $( [31-Oct-2007] $) $( [8-Aug-2005] $)

  ${
    $d w ps $. $d x ps $.  $d y ps $.  $d z ps $.
    $( Theorem 19.41 of [Margaris] p. 90 with 4 quantifiers. $)
    19.41vvvv $p |- ( E. w E. x E. y E. z ( ph /\ ps ) <->
                     ( E. w E. x E. y E. z ph /\ ps ) ) $=
      ( wa wex 19.41vvv exbii 19.41v bitr ) ABGEHDHCHZFHAEHDHCHZBGZFHNFHBGMOFAB
      CDEIJNBFKL $.
      $( [14-Jul-2007] $) $( [14-Jul-2007] $)
  $}

  ${ $d ch w x z $. $d ph x y $. $d ph x z $. $d ps w y $.
     $d ps w z $. $d th w x y $.
    $( Rearrange existential quantifiers. $)
    eeeeanv $p |- ( E. w E. x E. y E. z ( ( ph /\ ps /\ ch ) /\ th ) <->
                 ( ( E. w ph /\ E. x ps /\ E. y ch ) /\ E. z th ) ) $=
      ( w3a wa wex 19.41vvv ancom eeeanv anbi2i 3bitr exbii 19.41v bitr
      and4com 3exbi 3bitr3 ) BCDIZAJZGKFKEKZHKZAHKZBEKZCFKZDGKZIZJZABCIDJZGKFKE
      KZHKUGUHUIIUJJUFAUKJZHKULUEUOHUEUCGKFKEKZAJAUPJUOUCAEFGLUPAMUPUKABCDEFGNO
      PQAUKHRSUEUNHUDUMEFGUDAUCJUMUCAMABCDTSUAQUGUHUIUJTUB $.
      $( [14-Jul-2007] $) $( [14-Jul-2007] $)
  $}

  ${ $d x A $.
    r19.3rzvb.1 $e |- ( ph -> A. x ph ) $.
    $( A more general version of ~ r19.3rzv . $)
    r19.3rzvb $p |- ( A =/= (/) -> ( ph <-> A. x e. A ph ) ) $=
      ( c0 wne cv wcel wex wi wral wb ne0 biimt sylbi wal df-ral 19.23 bitr
      syl6bbr ) CEFZABGCHZBIZAJZABCKZUAUCAUDLBCMUCANOUEUBAJBPUDABCQUBABDRST $.
      $( [3-Jan-2008] $) $( [3-Jan-2008] $)
  $}

