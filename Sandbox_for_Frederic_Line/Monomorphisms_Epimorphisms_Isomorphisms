$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Monomorphisms, Epimorphisms, Isomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Epi $. $( The class of all epimorphisms. $)
  $c Monic $. $( The class of all monomorphisms. $)
  $c Iso $. $( The class of all isomorphisms. $)

  $( Extend class notation with the class of all epimorphisms. $)
  cepi $a class Epi $.

  $( Extend class notation with the class of all monomorphisms. $)
  cmon $a class Monic $.

  $( Extend class notation with the class of all isomorphisms. $)
  ciso $a class Iso $.

  ${ $d x y f g h $.
    $( Function returning the monomorphisms of a category id-est the left
       cancelable morphisms. JFM CAT_1 def. 10. $)
    df-mon $a |- Monic = { <. x , y >. |
    ( x e. Cat /\ y = { f | ( f e. dom ( dom_ ` x ) /\
      A. g e. dom ( dom_ ` x ) A. h e. dom ( dom_ ` x )
        ( ( ( ( dom_ ` x ) ` g ) = ( ( dom_ ` x ) ` h ) /\
            ( ( cod_ ` x ) ` g ) = ( ( dom_ ` x ) ` f ) /\
            ( ( cod_ ` x ) ` h ) = ( ( dom_ ` x ) ` f ) ) ->
              ( ( f ( o_ ` x ) g ) = ( f ( o_ ` x ) h ) -> g = h ) ) ) } ) } $.
  $}

  ${ $d C x y $. $d D x y $. $d M f g h x y $. $d R x y $.
     $d T f g h x y $.
    ismona.1 $e |- M = dom ( dom_ ` T ) $.
    ismona.2 $e |- D = ( dom_ ` T ) $.
    ismona.3 $e |- C = ( cod_ ` T ) $.
    ismona.4 $e |- R = ( o_ ` T ) $.
    $( Monomorphisms of a category. $)
    ismona $p |- ( T e. Cat -> ( Monic ` T ) = { f | ( f e. M /\
      A. g e. M A. h e. M
        ( ( ( D ` g ) = ( D ` h ) /\
            ( C ` g ) = ( D ` f ) /\
            ( C ` h ) = ( D ` f ) ) ->
              ( ( f R g ) = ( f R h ) -> g = h ) ) ) } ) $=
      ( vx vy cv cdom_ cfv cdm wcel wceq ccod_ w3a co_ co wi wral wa cab ccat
      cmon fveq2 dmeqd syl6eqr eleq2d raleq1d ralbidv fveq1d eqcomi fveq1i
      syl6eq eqeq12d 3anbi123d opreqd opreq eqcoms ax-mp eqeq12i syl6bb imbi1d
      imbi12d 2ralbidv 3bitrd anbi12d abbidv df-mon cvv fvex dmexg eqeltr
      zfausab fvopab4 ) MNDEOZMOZPQZRZSZFOZWDQZGOZWDQZTZWGWCUAQZQZWBWDQZTZWIWLQ
      ZWNTZUBZWBWGWCUCQZUDZWBWIWSUDZTZWGWITZUEZUEZGWEUFZFWEUFZUGZEUHWBHSZWGBQZW
      IBQZTZWGAQZWBBQZTZWIAQZXNTZUBZWBWGCUDZWBWICUDZTZXCUEZUEZGHUFFHUFZUGZEUHUI
      UJWCDTZXHYEEYFWFXIXGYDYFWEHWBYFWEDPQZRZHYFWDYGWCDPUKZULIUMZUNYFXGXEGHUFZF
      WEUFYKFHUFYDYFXFYKFWEYFXEGWEHYJUOUPYFYKFWEHYJUOYFXEYCFGHHYFWRXRXDYBYFWKXL
      WOXOWQXQYFWHXJWJXKYFWHWGYGQXJYFWGWDYGYIUQWGYGBBYGJURZUSUTYFWJWIYGQXKYFWIW
      DYGYIUQWIYGBYLUSUTVAYFWMXMWNXNYFWMWGDUAQZQXMYFWGWLYMWCDUAUKZUQWGYMAAYMKUR
      ZUSUTYFWNWBYGQXNYFWBWDYGYIUQWBYGBYLUSUTZVAYFWPXPWNXNYFWPWIYMQXPYFWIWLYMYN
      UQWIYMAYOUSUTYPVAVBYFXBYAXCYFXBWBWGDUCQZUDZWBWIYQUDZTYAYFWTYRXAYSYFWSYQWB
      WGWCDUCUKZVCYFWSYQWBWIYTVCVAYRXSYSXTCYQTZYRXSTZLUUBYQCWBWGYQCVDVEVFUUAYSX
      TTZLUUCYQCWBWIYQCVDVEVFVGVHVIVJVKVLVMVNMNEFGVOYDEHHYHVPIYGVPSYHVPSDPVQYGV
      PVRVFVSVTWA $.
      $( [5-Dec-2007] $) $( [5-Dec-2007] $)
  $}

  ${ $d C f $. $d D f $. $d F f g h $. $d M f g h $.
     $d R f $. $d T f g h $.
    ismonb.1 $e |- M = dom ( dom_ ` T ) $.
    ismonb.2 $e |- D = ( dom_ ` T ) $.
    ismonb.3 $e |- C = ( cod_ ` T ) $.
    ismonb.4 $e |- R = ( o_ ` T ) $.
    $( The predicate "is a monomorphism". $)
    ismonb $p |- ( ( T e. Cat /\ F e. A ) -> ( F e. ( Monic ` T )
      <-> ( F e. M /\ A. g e. M A. h e. M
        ( ( ( D ` g ) = ( D ` h ) /\
            ( C ` g ) = ( D ` F ) /\
            ( C ` h ) = ( D ` F ) ) ->
              ( ( F R g ) = ( F R h ) -> g = h ) ) ) ) ) $=
      ( vf ccat wcel wa cmon cfv cv wceq w3a co wi wral cab ismona adantr
      eleq2d wb eleq1 fveq2 eqeq2d 3anbi23d opreq1 eqeq12d imbi1d imbi12d
      2ralbidv anbi12d elabg adantl bitrd ) EOPZHAPZQZHERSZPHNTZIPZFTZCSGTZCSUA
      ZVJBSZVHCSZUAZVKBSZVNUAZUBZVHVJDUCZVHVKDUCZUAZVJVKUAZUDZUDZGIUEFIUEZQZNUF
      ZPZHIPZVLVMHCSZUAZVPWJUAZUBZHVJDUCZHVKDUCZUAZWBUDZUDZGIUEFIUEZQZVFVGWGHVD
      VGWGUAVEBCDENFGIJKLMUGUHUIVEWHWTUJVDWFWTNHAVHHUAZVIWIWEWSVHHIUKXAWDWRFGII
      XAVRWMWCWQXAVOWKVQWLVLXAVNWJVMVHHCULZUMXAVNWJVPXBUMUNXAWAWPWBXAVSWNVTWOVH
      HVJDUOVHHVKDUOUPUQURUSUTVAVBVC $.
      $( [3-Jan-2008] $) $( [2-Jan-2008] $)

    $( The predicate "is a monomorphism". $)
    ismonb1 $p |- ( ( T e. Cat /\ F e. M ) -> ( F e. ( Monic ` T ) <->
      A. g e. M A. h e. M
        ( ( ( D ` g ) = ( D ` h ) /\
            ( C ` g ) = ( D ` F ) /\
            ( C ` h ) = ( D ` F ) ) ->
              ( ( F R g ) = ( F R h ) -> g = h ) ) ) )  $=
      ( ccat wcel wa cmon cfv cv wceq w3a co wi wral ismonb wb ibar adantl
      bitr4d ) DMNZGHNZOGDPQNUJERZBQFRZBQSUKAQGBQZSULAQUMSTGUKCUAGULCUASUKULSUB
      UBFHUCEHUCZOZUNHABCDEFGHIJKLUDUJUNUOUEUIUJUNUFUGUH $.
      $( [3-Jan-2008] $) $( [2-Jan-2008] $)
  $}

  ${ $d C g j $. $d D g j $. $d F g j $. $d G g j $.
     $d J j $. $d M g j $. $d R g j $. $d T g j $.
    ismonb2.1 $e |- M = dom ( dom_ ` T ) $.
    ismonb2.2 $e |- D = ( dom_ ` T ) $.
    ismonb2.3 $e |- C = ( cod_ ` T ) $.
    ismonb2.4 $e |- R = ( o_ ` T ) $.
    $( The predicate "is a monomorphism". $)
    ismonb2 $p |- ( ( T e. Cat /\ ( F e. M /\ G e. M /\ J e. M ) ) ->
      ( F e. ( Monic ` T ) ->
        ( ( ( D ` G ) = ( D ` J ) /\
            ( C ` G ) = ( D ` F ) /\
            ( C ` J ) = ( D ` F ) ) ->
              ( ( F R G ) = ( F R J ) -> G = J ) ) ) )  $=
      ( vg vj ccat wcel w3a wa cmon cfv cv wceq co wi wral wb ismonb1
      3ad2antr1 3simpc adantl fveq2 eqeq1d 3anbi12d opreq2 eqeq1 imbi12d
      eqeq2d 3anbi13d eqeq2 rcla42v syl sylbid ) DOPZEHPZFHPZGHPZQZRZEDSTPZMUAZ
      BTZNUAZBTZUBZVJATZEBTZUBZVLATZVPUBZQZEVJCUCZEVLCUCZUBZVJVLUBZUDZUDZNHUEMH
      UEZFBTZGBTZUBZFATZVPUBZGATZVPUBZQZEFCUCZEGCUCZUBZFGUBZUDZUDZVCVEVDVIWGUFV
      FABCDMNEHIJKLUGUHVHVEVFRZWGXAUDVGXBVCVDVEVFUIUJWFXAWHVMUBZWLVSQZWPWBUBZFV
      LUBZUDZUDMNFGHHVJFUBZVTXDWEXGXHVNXCVQWLVSXHVKWHVMVJFBUKULXHVOWKVPVJFAUKUL
      UMXHWCXEWDXFXHWAWPWBVJFECUNULVJFVLUOUPUPVLGUBZXDWOXGWTXIXCWJVSWNWLXIVMWIW
      HVLGBUKUQXIVRWMVPVLGAUKULURXIXEWRXFWSXIWBWQWPVLGECUNUQVLGFUSUPUPUTVAVB $.
      $( [3-Jan-2008] $) $( [2-Jan-2008] $)
  $}

  ${ $d B a g h $. $d C a g h $. $d F a g h $.
     $d H a g h $. $d O a g h $. $d R a $. $d T a g h $.
    imonclem.1 $e |- O = dom ( id_ ` T ) $.
    imonclem.2 $e |- H = ( hom ` T ) $.
    imonclem.3 $e |- R = ( o_ ` T ) $.
    $( Lemma for ~ ismonc . $)
    imonclem $p |- (  (  T e. Cat
                         /\ ( B e. O /\ C e. O )
                         /\ F e. ( H ` <. B , C >. ) )
                      -> (  A. a
                            e. O
                               A. g
                               e. ( H ` <. a , B >. )
                                  A. h
                                  e. ( H ` <. a , B >. )
                                     ( ( F R g ) = ( F R h ) -> g = h )
                         -> (  F e. dom ( dom_ ` T )
                            /\ A. g
                               e. dom ( dom_ ` T )
                                  A. h
                                  e. dom ( dom_ ` T )
                                     (  (    ( ( dom_ ` T ) ` g )
                                           = ( ( dom_ ` T ) ` h )
                                        /\   ( ( cod_ ` T ) ` g )
                                           = ( ( dom_ ` T ) ` F )
                                        /\   ( ( cod_ ` T ) ` h )
                                           = ( ( dom_ ` T ) ` F ) )
                                     -> ( ( F R g ) = ( F R h ) -> g = h ) ) )
                         ) ) $=
      ( ccat wcel wa cop cfv w3a cv co wceq wi wral cdom_ cdm ccod_ eqid ehm
      3expib 3imp adantr wal ax-17 hbra1 hbral hban dmo expcom 3simp1 syl5com
      imp r2al ralbii wb opeq1 fveq2d eleq2d anbi12d imbi1d adantl ishomd
      ad2antrr 3simp2 pm3.26d syl3anc eqcomd ad2antll eqtr3t ex 3ad2ant3 com12
      3jca simprl a1i eqtrt 3ad2ant2 simprr jca impbida dehm simplr sylanc
      3bitrd rcla4dv ax-4 a4s r19.20si sylbi mpdan com23 19.21ai sylibr ) DNOZA
      IOZBIOZPZGABQHROZSZGETZCUAGFTZCUAUBXJXKUBUCZFJTZAQZHRZUDZEXOUDZJIUDZGDUER
      ZUFZOZXJXSRZXKXSRZUBZXJDUGRZRZGXSRZUBZXKYERZYGUBZSZXLUCZFXTUDEXTUDZPXIXRP
      ZYAYMXIYAXRXDXGXHYAXDXEXFXHYAUCABDGHXTIKXTUHZLUIUJUKULYNXJXTOZXKXTOZPZYLU
      CZFUMZEUMYMYNYTEXIXREXIEUNXQEJIXMIOZEUNXPEXOUOUPUQYNYSFXIXRFXIFUNXQFJIUUA
      FUNXPFEXOXJXOOZFUNXLFXOUOUPUPUQXIXRYSXIYRXRYLXIYRXRYLUCZXIYRPZYBIOZUUCXIY
      RUUEYRXDUUEXIYPXDUUEUCYQXDYPUUEXSDXJXTIYOKXSUHZURUSULXDXGXHUTZVAVBZXRUUDU
      UEPZYLXRUUBXKXOOZPZXLUCZFUMZEUMZJIUDZUUIYLUCXQUUNJIXLEFXOXOVCVDUUIUULJIUD
      YLUUOUUDUULYLJYBIUUDXMYBUBZPZUULXJYBAQZHRZOZXKUUSOZPZXLUCZYPYBYBUBZYFAUBZ
      SZYQYCYBUBZYIAUBZSZPZXLUCZYLUUPUULUVCVEUUDUUPUUKUVBXLUUPUUBUUTUUJUVAUUPXO
      UUSXJUUPXNUURHXMYBAVFVGZVHUUPXOUUSXKUVLVHVIVJVKXDUUEXEUVCUVKVEUUQXDUUEXES
      ZUVBUVJXLUVMUUTUVFUVAUVIYBAYEXSDXJHXTIKYOUUFYEUHZLVLYBAYEXSDXKHXTIKYOUUFU
      VNLVLVIVJXIXDYRUUPUUGVMUUDUUEUUPUUHULXIXEYRUUPXIXEXFXDXGXHVNVOVMVPUUQUVJY
      KXLYGAUBZYRUVJYKVEUUQUVOYRPZUVJYKUVPUVJPYDYHYJUVIYDUVPUVFUVIYCYBYQUVGUVHV
      NVQVRUVPUVJYHUVOUVJYHUCYRUVJUVOYHUVFUVOYHUCZUVIUVEYPUVQUVDUVEUVOYHYFYGAVS
      VTWAULWBULVBUVPUVJYJUVOUVJYJUCYRUVJUVOYJUVIUVOYJUCZUVFUVHYQUVRUVGUVHUVOYJ
      YIYGAVSVTWAVKWBULVBWCUVPYKPZUVFUVIUVSYPUVDUVEUVPYPYKUVOYPYQWDULUVDUVSYBUH
      WEUVPYKUVEUVOYKUVEUCYRYKUVOUVEYHYDUVOUVEUCYJYHUVOUVEYFYGAWFVTWGWBULVBWCUV
      SYQUVGUVHUVPYQYKUVOYPYQWHULYKUVGUVPYKYBYCYDYHYJUTVQVKUVPYKUVHUVOYKUVHUCYR
      YKUVOUVHYJYDUVOUVHUCYHYJUVOUVHYIYGAWFVTWAWBULVBWCWIWJXIUVOYRUUPXDXGXHUVOX
      DXEXFXHUVOUCABXSDGHIKUUFLWKUJUKVMXIYRUUPWLWMVJWNWOUUNUULJIUUMUULEUULFWPWQ
      WRVAWSWBWTVTXAVBXBXBYLEFXTXTVCXCWIVT $.
      $( [3-Jan-2008] $) $( [1-Jan-2008] $)
  $}

  ${ $d B a g h $. $d C a g h $. $d F a g h $.
     $d H a g h $. $d O a g h $. $d R a $. $d T a g h $.
    ismonc.1 $e |- O = dom ( id_ ` T ) $.
    ismonc.2 $e |- H = ( hom ` T ) $.
    ismonc.3 $e |- R = ( o_ ` T ) $.
    $( The predicate "is a monomorphism" when ` F ` belongs to a homset .
   $)
    ismonc $p |- ( ( T e. Cat /\ ( B e. O /\ C e. O ) /\
      F e. ( H ` <. B , C >. ) ) -> ( F e. ( Monic ` T ) <->
        A. a e. O A. g e. ( H ` <. a , B >. ) A. h e. ( H ` <. a , B >. )
        ( ( F R g ) = ( F R h ) -> g = h ) ) ) $=
      ( ccat wcel wa cop cfv w3a cmon cdom_ cdm cv wceq ccod_ co wi wral wb
      eqid ismonb 3adant2 wal ax-17 hbra1 hban hbral ra42 pm3.31 ishomd biimpd
      anim12d 3simp1 ad2antrr simplr 3simp2 pm3.26d syl3anc anim12i a1i dehm
      eqtr3t syl2an adantl ex 3ad2ant3 adantr impcom 3jca syl6 3expib 3imp
      jcad syld imim1d com13 3syl 19.21ai exp31 com23 imp 19.21aiv r2al ralbii
      df-ral bitr sylibr imonclem impbid bitrd ) DNOZAIOZBIOZPZGABQHRZOZSZGDTRO
      ZGDUARZUBZOZEUCZXIRZFUCZXIRZUDZXLDUERZRZGXIRZUDZXNXQRZXSUDZSZGXLCUFGXNCUF
      UDXLXNUDUGZUGZFXJUHZEXJUHZPZYDFJUCZAQHRZUHEYJUHZJIUHZXAXFXHYHUIXDXEXQXICD
      EFGXJXJUJZXIUJZXQUJZMUKULXGYHYLXGYHYLXGYHPZYIIOZXLYJOZXNYJOZPZYDUGZFUMZEU
      MZUGZJUMZYLYPUUDJXGYHUUDXGYQYHUUCXGYQYHUUCXGYQPZYHPZUUBEUUFYHEUUFEUNXKYGE
      XKEUNYFEXJUOUPUPUUGUUAFUUFYHFUUFFUNXKYGFXKFUNYFFEXJXLXJOZFUNYEFXJUOUQUPUP
      YHUUFUUAYGXKUUFUUAUGZYGUUHXNXJOZPZYEUGUUKYCPZYDUGZXKUUIUGYEEFXJXJURUUKYCY
      DUSUUFXKUUMUUAUUFXKUUMUUAUGUUFXKPZYTUULYDUUNYTUUHXMYIUDZXRAUDZSZUUJXOYIUD
      ZYAAUDZSZPZUULXAYQXBYTUVAUGUUNXAYQXBSZYRUUQYSUUTUVBYRUUQYIAXQXIDXLHXJIKYM
      YNYOLUTVAUVBYSUUTYIAXQXIDXNHXJIKYMYNYOLUTVAVBXGXAYQXKXAXDXFVCVDXGYQXKVEXG
      XBYQXKXGXBXCXAXDXFVFVGVDVHUUNUVAUUKYCUVAUUKUGUUNUUQUUHUUTUUJUUHUUOUUPVCUU
      JUURUUSVCVIVJXGUVAYCUGZYQXKXAXDXFUVCXAXBXCXFUVCUGXAXBXCSXFXSAUDZUVCABXIDG
      HIKYNLVKUVDUVAYCUVDUVAPXPXTYBUVAXPUVDUUOUURXPUUQUUTXMXOYIVLUUHUUOUUPVFUUJ
      UURUUSVFVMVNUVAUVDXTUUQUVDXTUGZUUTUUPUUHUVEUUOUUPUVDXTXRXSAVLVOVPVQVRUVAU
      VDYBUUTUVDYBUGZUUQUUSUUJUVFUURUUSUVDYBYAXSAVLVOVPVNVRVSVOVTWAWBVDWCWDWEVO
      WFWGVRVRWHWHWIWJWKWLYLUUCJIUHUUEYKUUCJIYDEFYJYJWMWNUUCJIWOWPWQVOABCDEFGHI
      JKLMWRWSWT $.
      $( [3-Jan-2008] $) $( [2-Jan-2008] $)
  $}


  ${  $d A a g h $. $d B a g h k l $. $d C a g h k l $. $d F a g h k l $.
      $d G a g h k l $. $d H a g h k l $. $d O a g h k l $. $d R a g h k l $.
      $d T a g h k l $.

    cmpmon.1 $e |- O = dom ( id_ ` T ) $.
    cmpmon.2 $e |- H = ( hom ` T ) $.
    cmpmon.3 $e |- R = ( o_ ` T ) $.
    $( The composite of two monomorphisms is a monomorphism.
       JFM CAT_1 th. 61 $)
    cmpmon $p |- ( ( T e. Cat /\ ( ( A e. O /\ B e. O /\ C e. O ) /\
         ( F e. ( H ` <. A , B >. ) /\ G e. ( H ` <. B , C >. ) )
           /\ ( F e. ( Monic ` T ) /\ G e. ( Monic ` T ) ) ) ) ->
              ( G R F ) e. ( Monic ` T ) ) $=
      ( vg vh va vk vl ccat wcel w3a cop cfv wa cmon co wi cv wceq wral ax-17
      hbra1 hban hbral cmpassoh 3expib com3r com23 com14 exp43 3impd com13
      com34 3imp adantr imp31 ad2antrr eqcom biimp adantl imp eqcomd 3exp1
      com3l ex com12 com4r impcom 3impia 3eqtr2d opreq2 eqeq1d eqeq1 imbi12d
      eqeq2d eqeq2 rcla42v homgrf 3simpa syl5 adantrd com24 3adant3 jca ra4
      ad2antll sylc wal r3al 19.21bi 19.21bbi ad2antrl 3expd imp41 syld
      r19.21ai wb ismonc 3simp1 3simpb 3ad2ant2 syl3anc sylibrd biimpd 3exp
      3simpc syl2and 3imp2 ) ERSZAISZBISZCISZTZFABUAHUBSZGBCUAHUBSZUCZFEUDUBZSZ
      GYFSZUCZGFDUEZYFSZXRYBYEYIYKUFXRYBYETZFMUGZDUEZFNUGZDUEZUHZYMYOUHZUFZNOUG
      ZAUAHUBZUIZMUUAUIZOIUIZGPUGZDUEZGQUGZDUEZUHZUUEUUGUHZUFZQYTBUAHUBZUIPUULU
      IZOIUIZYKYGYHYLUUDUUNUCZYJYMDUEZYJYODUEZUHZYRUFZNUUAUIZMUUAUIZOIUIZYKYLUU
      OUVBYLUUOUCZUVAOIYLUUOOYLOUJUUDUUNOUUCOIUKUUMOIUKULULUVCYTISZUVAUVCUVDUCZ
      UUTMUUAUVCUVDMYLUUOMYLMUJUUDUUNMUUCMOIUVDMUJZUUBMUUAUKUMUUNMUJULULUVFULUV
      EYMUUASZUUTUVEUVGUCZUUSNUUAUVEUVGNUVCUVDNYLUUONYLNUJUUDUUNNUUCNOIUVDNUJZU
      UBNMUUAUVGNUJZYSNUUAUKUMUMUUNNUJULULUVIULUVJULUVHYOUUASZUUSUVHUVKUCZUURGY
      NDUEZGYPDUEZUHZYRUVLUURUVOUVLUURUCUVMUUPUUQUVNUVHUVMUUPUHZUVKUURUVCUVDUVG
      UVPYLUVDUVGUVPUFZUFZUUOXRYBYEUVRXRYBUVDYEUVQUVDYBXRYEUVQUFZUVDXSXTYAXRUVS
      UFZUVDXSXTYAUVTUVGXRYEUVDXSUCZXTYAUCZUCZUVPUVGYEXRUWCUVPUFZUVGYCYDXRUWDUF
      XRUWCUVGYCYDTZUVPXRUWAUWBUWEUVPUFYTABCDEHYMFGIJKLUNUOUPUOUQURUSUTVAVBVCVD
      VEVFUURUUQUUPUHZUVLUURUWFUUPUUQVGVHVIUVLUUQUVNUHZUURUVHUVKUWGUVEUVKUWGUFZ
      UVGUVCUVDUWHYLUVDUWHUFZUUOXRYBYEUWIUVKYEUVDXRYBUCZUWGUVKYCYDUVDUWJUWGUFUF
      UWJUVDUVKYCYDTZUWGYBXRUVDUWKUWGUFZUFZXSXTYAXRUWMUFXTYAXRXSUWMXTYAXRXSUWMU
      FZUFXRUWBUWNUVDUWBXSXRUWLUVDXSUWBXRUWLUFZUVDXSUWBUWOUFXRUWAUWBUWLXRUWAUWB
      UWKUWGXRUWAUWBTZUWKUCUVNUUQUWPUWKUVNUUQUHYTABCDEHYOFGIJKLUNVJVKVLVMVNUQUR
      VOVNVPVCVQVAUOURVRVDVJVDVJVDVSVNUVLUVOYQYRYNUULSZYPUULSZUCUUMUVOYQUFZUVLU
      UKUWSUVMUUHUHZYNUUGUHZUFPQYNYPUULUULUUEYNUHZUUIUWTUUJUXAUXBUUFUVMUUHUUEYN
      GDVTWAUUEYNUUGWBWCUUGYPUHZUWTUVOUXAYQUXCUUHUVNUVMUUGYPGDVTWDUUGYPYNWEWCWF
      UVLUWQUWRUVHUWQUVKUVCUVDUVGUWQYLUVDUVGUWQUFZUFZUUOXRYBYEUXEYEYBXRUXEYCYBX
      RUXEUFZUFYDYCXSXTUCZUXFYBUVDUXGXRYCUXDUVDXSXTXRYCUXDUFUFUVGXRYCUVDXSXTTZU
      WQUVGYCXRUXHUWQUFZUVGYCXRUXIUFXRUXHUVGYCUCZUWQXRUXHUXJUWQUFYTABDEYMFHIJKL
      WGVNUPVNUQURUOURXSXTYAWHZWIVDVAVCVDVEVDUVHUVKUWRUVEUVKUWRUFZUVGUVCUVDUXLY
      LUVDUXLUFZUUOXRYBYEUXMYBXRYEUXMUFZXSXTXRUXNUFYAUXGUVDYEXRUXLUVDUXGYEXRUXL
      UFZUFZUVDXSXTUXPUXHYCUXOYDUVKYCXRUXHUWRUVKYCXRUXHUWRUFUFXRUXHUVKYCUCZUWRX
      RUXHUXQUWRUFYTABDEYOFHIJKLWGVNUPVNURWJUOVOWKWLVOVCVDVJVDVJWMUVEUUMUVGUVKU
      VCUVDUUMUUNUVDUUMUFYLUUDUUMOIWNWOVJVFWPUVCUVDUVGUVKYSUVCUVDUVGUVKYSUUDUVD
      UVGUVKTYSUFZYLUUNUUDUXRMNUUDUXRNWQMWQZOUUDUXSOWQYSOMNIUUAUUAWRVHWSWTXAXBX
      CXDXDVNXEVNXEVNXEVNXRXSYAUCZYJACUAHUBSZYKUVBXFYLACDEMNYJHIOJKLXGXRYBYEXHY
      BXRUXTYEXSXTYAXIXJXRYBYEUYAABCDEFGHIJKLWGVRXKXLXRYBYEYGUUDUFZXRUXGYEUYBUF
      YBYEUXGXRUYBYCUXGXRUYBUFUFYDXRUXGYCUYBXRUXGYCUYBXRUXGYCTYGUUDABDEMNFHIOJK
      LXGXMXNVAVDVAUXKWIVCXRYBYEYHUUNUFZXRUWBYEUYCUFYBYEUWBXRUYCYDUWBXRUYCUFUFY
      CXRUWBYDUYCXRUWBYDUYCXRUWBYDTYHUUNBCDEPQGHIOJKLXGXMXNVAVIVAXSXTYAXOWIVCXP
      XNXQ $.
      $( [10-Mar-2008] $) $( [10-Mar-2008] $)
  $}


  ${ $d x y f g h $.
    $( Function returning the epimorphisms of a category id-est the left
       cancelable morphisms. The joy of cats def 7.39 p. 106. $)
    df-epi $a |- Epi = { <. x , y >. |
    ( x e. Cat /\ y = { f | ( f e. ran ( o_ ` x ) /\
      A. g e. ran ( o_ ` x ) A. h e. ran ( o_ ` x )
        ( ( ( ( cod_ ` x ) ` g ) = ( ( cod_ ` x ) ` h ) /\
            ( ( dom_ ` x ) ` g ) = ( ( cod_ ` x ) ` f ) /\
            ( ( dom_ ` x ) ` h ) = ( ( cod_ ` x ) ` f ) ) ->
              ( ( g ( o_ ` x ) f ) = ( h ( o_ ` x ) f ) -> g = h ) ) ) } ) } $.

  $}

