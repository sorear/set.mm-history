$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Separated spaces: T0, T1, T2 (Hausdorff) ...
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c T0 $. $( The class of all the T0-spaces. $)
  $c T1 $. $( The class of all the T1-spaces. $)

  $( Extend class notation to include T0-spaces. $)
  ct0 $a class T0 $.
  $( Extend class notation to include T1-spaces. $)
  ct1 $a class T1 $.

  ${
    $d a b x u $.
    $( The class of all T0-spaces also called Kolmogorov spaces.
    Morris. Topology without tears. p. 30 ex. 5. $)
    df-t0 $a |- T0 = { x e. Top | A. a e. U. x A. b e. U. x E. u e. x
      ( ( a e. u /\ -. b e. u ) \/ ( -. a e. u /\ b e. u ) ) } $.
  $}

  ${
    $d x a $.
    $( The class of all T1-spaces also called Frechet spaces.
    Morris. Topology without tears. p. 30 ex. 3. $)
    df-t1 $a |- T1 = { x e. Top | A. a e. U. x { a } e. ( Clsd ` x ) } $.
  $}

  ${
    $d J a x $. $d X x $.
    ist1.1 $e |- X = U. J $.
    $( The predicate ` J ` is T1. $)
    ist1 $p |- ( J e. T1 <->
      ( J e. Top /\ A. a e. X { a } e. ( Clsd ` J ) ) ) $=
      ( vx cv csn ccld cfv wcel cuni wral ctop ct1 wceq unieq syl6eqr eleq2d
      fveq2 imbi12d ralbidv2 df-t1 elrab2 ) CFZGZEFZHIZJZCUFKZLUEAHIZJZCBLEAMNU
      FAOZUHUKCUIBULUDUIJUDBJUHUKULUIBUDULUIAKBUFAPDQRULUGUJUEUFAHSRTUAECUBUC
      $.
      $( [18-Jun-2007] $) $( [7-Jun-2007] $)
  $}

  ${
    $d A x $.
    dtopcl.1 $e |- A e. V $.
    $( The open sets of a discrete topology are closed and its closed sets
       are open. $)
    dtopcl $p |- P~ A = ( Clsd ` P~ A ) $=
      ( vx cpw ccld cfv ctop wcel cv wb distop cuni wss cdif wa eqid iscld
      unipw sseq2i biimp visset elpw sylibr adantr elssuni difss sseqtr cvv
      eqeltr difexg ax-mp mpbir jctir impbi syl6rbb eqriv ) CADZUQEFZUQGHZCIZUQ
      HZUTURHZJABKUSVBUTUQLZMZVCUTNZUQHZOZVAUTUQVCVCPQVGVAVDVAVFVDUTAMZVAVDVHVC
      AUTARZSTUTACUAUBUCUDVAVDVFUTUQUEVFVEAMVEVCAVCUTUFVIUGVEAVCUHHVEUHHVCAUHVI
      BUIVCUTUHUJUKUBULUMUNUOUKUP $.
      $( [18-Jun-2007] $) $( [7-Jun-2007] $)
  $}

  ${
    $d J x $.
    $( A Hausdorff space is a T1 space. $)
    t2t1 $p |- ( J e. Haus -> J e. T1 ) $=
      ( vx cha wcel ctop cv csn ccld cfv cuni wral wa ct1 haustop eqid sncld
      r19.21aiva jca ist1 sylibr ) ACDZAEDZBFZGAHIDZBAJZKZLAMDUAUBUFANUAUDBUEUC
      AUEUEOZPQRAUEBUGST $.
      $( [18-Jun-2007] $) $( [11-Jun-2007] $)
  $}

  $( A Hausdorff space is a T1 space. $)
  hst1 $p |- Haus (_ T1 $=
    ( vx cha ct1 wss cv wcel wi dfss2 t2t1 mpgbir ) BCDAEZBFKCFGAABCHKIJ $.
    $( [20-Jul-2007] $) $( [18-Jun-2007] $)

  ${
    $d A u v x y $.
    dtt2.1 $e |- A e. V $.
    $( A discrete topology is Hausdorff. Morris. Topology without tears.
       p.72. ex. 13. $)
    dtt2 $p |- P~ A e. Haus $=
      ( vx vy vu vv cpw cha wcel ctop cv wne cin c0 wceq w3a wrex wi cuni wral
      eqid ishaus distop wa csn visset snid a1i disjsn2 3jca eleq2 ineq1
      eqeq1d 3anbi13d imbi2d ineq2 3anbi23d rcla42ev 3expia unipw eleq2i biimp
      snelpw sylib syl2an mpi r19.37av r19.22si 3syl rgen2a mpbir2an ) AGZHIVLJ
      ICKZDKZLZVMEKZIZVNFKZIZVPVRMZNOZPZFVLQZEVLQRZDVLSZTCWETCDFEVLWEWEUAUBABUC
      WDCDWEVMWEIZVNWEIZUDZVOWBRZFVLQZEVLQZVOWCRZEVLQWDWHVOVMVMUEZIZVNVNUEZIZWM
      WOMZNOZPZRZWKVOWNWPWRWNVOVMCUFZUGUHWPVOVNDUFZUGUHVMVNUIUJWMVLIZWOVLIZWTWK
      RWFWGXCXDWTWKWIWTVOWNVSWMVRMZNOZPZREFWMWOVLVLVPWMOZWBXGVOXHVQWNWAXFVSVPWM
      VMUKXHVTXENVPWMVRULUMUNUOVRWOOZXGWSVOXIVSWPXFWRWNVRWOVNUKXIXEWQNVRWOWMUPU
      MUQUOURUSWFVMAIZXCWFXJWEAVMAUTZVAVBVMAXAVCVDWGVNAIZXDWGXLWEAVNXKVAVBVNAXB
      VCVDVEVFWJWLEVLVOWBFVLVGVHVOWCEVLVGVIVJVK $.
      $( [14-Jul-2007] $) $( [24-Jun-2007] $)
  $}

  ${
    dtt1.1 $e |- A e. V $.
    $( A discrete topology is T1. Morris. Topology without tears. $)
    dtt1 $p |- P~ A e. T1 $=
      ( cpw cha wcel ct1 dtt2 t2t1 ax-mp ) ACZDEJFEABGJHI $.
      $( [18-Jun-2007] $) $( [8-Jun-2007] $)
   $}

