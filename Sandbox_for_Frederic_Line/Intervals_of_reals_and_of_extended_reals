$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Intervals of reals and of extended reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $. $d B x $. $( ` x ` is dummy. $)
    $( An open interval is included in ` RR ` if ` A ` and
       ` B ` belong to ` RR* ` . $)
    iooirrsa $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) (_ RR ) $=
      ( cxr wcel wa cioo co cr cpw wss ioof foprcl elpwi syl ) ACDBCDEABFGZHIZD
      OHJABPCCFKLOHMN $.
      $( [3-Mar-2007] $) $( [3-Mar-2007] $)
  $}


  ${
    $d A x $. $d B x $.
    $( A closed interval is a set of extended reals. $)
    clsrebb $p |- ( B e. V -> ( A [,] B ) (_ RR* ) $=
      ( vx cvv wcel cxr wa cicc co wss wn c0 cxp cpw wf cdm wceq iccf fdm
      ax-mp ndmoprgOLD 0ss a1i eqsstrd ex cv cle wbr w3a elicc1t 3simp1 syl6bi
      ssrdv pm2.61d2 ) BDEZAFEBFEGZABHIZFJZUOUPKZURUOUSGZUQLFABDFHFFMZFNZHOHPVA
      QRVAVBHSTUALFJUTFUBUCUDUEUPCUQFUPCUFZUQEVCFEZAVCUGUHZVCBUGUHZUIVDABVCUJVD
      VEVFUKULUMUN $.
      $( [18-Jun-2007] $) $( [13-Jun-2007] $)
  $}

  ${
    $d A x $. $d B x $.
    $( The class difference of ` RR ` and a closed interval. $)
    cdrci $p |- ( ( A e. RR /\ B e. RR ) -> ( RR \ ( A [,] B ) ) =
      U. { ( -oo (,) A ) , ( B (,) +oo ) } ) $=
      ( vx cr wcel wa cv cicc co wn cmnf cioo cpnf cpr cuni wb wal cdif wceq
      cun wo wi cxr cle wbr w3a elicc1t rexrt syl2an adantr negbid pm2.24d
      adantl clt ad2antlr mnfltt ltnlet bicomd pm3.27 simpll sylanc biimpa
      3jca ad2antrr mnfxr jctil elioo1t syl mpbird ex adantll ltpnft pnfxr
      jctir sylbird orim12d ianor syl5ib jaod 3anass negbii sylbid imp3a elun
      syl6ibr ioossre unssi sseli elioo2t syl5 ax-mp biimpd a1i com13 3impd
      xrltnlet com3l com14 imp sylibr intnand anim12i jca impbid cvv oprex
      pm3.2i uniprg eqcomd eleq2d bitrd 19.21aiv difeqri2 ) ADEZBDEZFZCGZDEZXQA
      BHIZEZJZFZXQKALIZBMLIZNOZEZPZCQDXSRYESXPYGCXPYBXQYCYDTZEZYFXPYBYIXPYBXQYC
      EZXQYDEZUAZYIXPXRYAYLXPXRYAYLUBXPXRFZYAXQUCEZAXQUDUEZXQBUDUEZUFZJZYLYMXTY
      QXPXTYQPZXRAUCEZBUCEZYSXNXOABXQUGZAUHZBUHZUIUJUKYMYNYOYPFZFZJZYLYRYMYNJZU
      UEJZUAYLUUGYMUUHYLUUIXRUUHYLUBXPXRYNYLXQUHZULUMYMYOJZYPJZUAZYLUUIYMUUKYJU
      ULYKYMUUKYJYMUUKFZYJYNKXQUNUEZXQAUNUEZUFZUUNYNUUOUUPXRYNXPUUKUUJUOXRUUOXP
      UUKXQUPUOYMUUKUUPXRXNUUKUUPPYMXRXNFZUUPUUKXQAUQZURXPXRUSXNXOXRUTVAVBVCUUN
      KUCEZYTFYJUUQPUUNYTUUTXPYTXRUUKXNYTXOUUCUJVDVEVFKAXQVGVHVIVJYMUULBXQUNUEZ
      YKXOXRUVAUULPXNBXQUQVKYMUVAYKYMUVAFZYKYNUVAXQMUNUEZUFZUVBYNUVAUVCXRYNXPUV
      AUUJUOYMUVAUSXRUVCXPUVAXQVLUOVCUVBUUAMUCEZFZYKUVDPZUVBUUAUVEXPUUAXRUVAXOU
      UAXNUUDUMZVDVMVNBMXQVGZVHVIVJVOVPYOYPVQZVRVSYNUUEVQVRYQUUFYNYOYPVTWAZVRWB
      VJWCXQYCYDWDZWEXPYIYBXPYIFZXRYAYIXRXPYHDXQYCYDDKAWFBMWFWGWHUMUVMYAYRUVMUU
      GYRUVMUUEYNUVMUUMUUIXPYIUUMXPYLUUMYIXPYJUUKYKUULXPYJXRUUOUUPUFZUUKXNYJUVN
      PZXOUUTXNUVOUBVEUUTYTUVOXNUUTYTUVOKAXQWIVJUUCWJWKUJXPXRUUOUUPUUKXNXRUUOUU
      PUUKUBZUBUBXOUUOXRXNUVPXRXNUVPUBUBUUOXRXNUVPUURUUPUUKUUSWLVJWMWNUJWOWBXPY
      KUVDUULXPUVFUVGXPUUAUVEUVHVMVNUVIVHXPYNUVAUVCUULXOYNUVAUVCUULUBUBUBZXNXOU
      UAUVQUUDUVCYNUVAUUAUULYNUVAUUAUULUBUBUBUVCUUAYNUVAUULUUAYNUVAUULUBUUAYNFU
      VAUULBXQWPWLVJWQWMWRVHUMWOWBVPUVLVRWSUVJWTXAUVKWTUVMYTUUAFZYAYRPXPUVRYIXN
      YTXOUUAUUCUUDXBUJUVRXTYQUUBUKVHVIXCVJXDXPYHYEXQXPYCXEEZYDXEEZFZYHYESUWAXP
      UVSUVTKALXFBMLXFXGWMUWAYEYHYCYDXEXEXHXIVHXJXKXLCDXSYEXMVH $.
      $( [18-Jun-2007] $) $( [7-Jun-2007] $)
  $}

  ${
  $d A x y z $. $d w x y z $.
  $( Membership to the set of open intervals implied the existence of
  two bounds in the set of the extended reals. $)
  bsi $p |- ( A e. ran (,) ->
      E. x e. RR* E. y e. RR* A = ( x (,) y ) ) $=
    ( vw vz cioo crn wcel cv co wceq cxr wrex wa clt wbr crab wex cab eqeq1
    anbi2d 2exbidv ceqsexgv biimpd clelab syl5ib copab2 df-ioo rneqi eleq2i
    rnoprab bitr ioovalt eqcomd eqeq2d imdistani 19.22i2 r2ex sylibr syl6
    pm2.43i ) CFGZHZCAIZBIZFJZKZBLMALMZVCVCVDLHVELHNZCVDDIZOPVJVEOPNDLQZKZNZBRA
    RZVHVCCVIEIZVKKZNZBRARZESZHZVNVCVCVOCKZVRNERZVNVTVCWBVNVRVNECVBWAVQVMABWAVP
    VLVIVOCVKTUAUBUCUDVRECUEUFVCCVQABEUGZGZHVTVBWDCFWCABEDUHUIUJWDVSCVQABEUKUJU
    LUFVNVIVGNZBRARVHVMWEABVIVLVGVIVLVGVIVKVFCVIVFVKDVDVEUMUNUOUDUPUQVGABLLURUS
    UTVA $.
    $( [29-Nov-2007] $) $( [31-Oct-2007] $)
  $}

  $( If an open interval has an element, then ` A < B ` . $)
  elioo1t3 $p |- ( C e. ( A (,) B ) -> A < B ) $=
    ( cxr wcel wa cioo co clt wbr wi w3a elioo1t xrlttrt exp3a 3com23 3expia
    3impd sylbid wn c0 wceq ndmioo eleq2 noel pm2.21i syl6bi syl pm2.61i ) ADEZ
    BDEZFZCABGHZEZABIJZKZULUNCDEZACIJZCBIJZLUOABCMULUQURUSUOUJUKUQURUSUOKKZUJUQ
    UKUTUJUQUKLURUSUOACBNOPQRSULTUMUAUBZUPABUCVAUNCUAEZUOUMUACUDVBUOCUEUFUGUHUI
    $.
    $( [14-Sep-2007] $) $( [14-Aug-2007] $)

  $( An open interval with its upper bound equal to ` -oo ` is empty. $)
  oisbmi $p |- ( A (,) -oo ) = (/) $=
    ( cxr wcel cmnf wa cioo co c0 wceq cle wbr mnflet adantr ioo0t mpbird
    ndmioo pm2.61i ) ABCZDBCZEZADFGHIZTUADAJKZRUBSALMADNOADPQ $.
    $( [14-Sep-2007] $) $( [12-Sep-2007] $)

  $( An open interval with its lower bound equal to ` +oo ` is empty. $)
  oisbmj $p |- ( +oo (,) A ) = (/) $=
    ( cpnf cxr wcel wa cioo co c0 wceq cle wbr pnfget adantl ioo0t mpbird
    ndmioo pm2.61i ) BCDZACDZEZBAFGHIZTUAABJKZSUBRALMBANOBAPQ $.
    $( [14-Sep-2007] $) $( [12-Sep-2007] $)

  $( Translation in a half-infinite interval. $)
  truni1 $p |- ( ( A e. RR* /\ D e. RR /\ 0 < D ) ->
   ( C e. ( A (,) +oo ) -> ( C + D ) e. ( A (,) +oo ) ) ) $=
    ( cxr wcel cr cc0 clt wbr w3a cpnf caddc co cioo wa wi axaddrcl ex
    3ad2ant1 com12 3ad2ant2 imp xrlttrt 3simp1 adantr rexrt adantl syl 3jca
    3simp2 ltaddpos2t biimpd com23 imp31 cc wceq axaddcom recnt ad2antrr
    sylanc breqtrrd expcom 3adant1 jca sylc ltpnft wb pnfxr jctir elioo2t
    3imtr4d ) ADEZCFEZGCHIZJZBFEZABHIZBKHIZJZBCLMZFEZAVTHIZVTKHIZJZBAKNMZEZVTWE
    EZVOVSWDVOVSOZWAWBWCVOVSWAVMVLVSWAPVNVSVMWAVPVQVMWAPVRVPVMWABCQZRSTUAUBVLBD
    EZVTDEZJVQBVTHIZOWBWHABVTUCWHVLWJWKVOVLVSVLVMVNUDZUEVSWJVOVPVQWJVRBUFSUGVOV
    SWKVMVLVSWKPVNVSVMWKVPVQVMWKPVRVPVMWKVPVMOZWAWKWIVTUFUHRSTUAUBUIWHVQWLVSVQV
    OVPVQVRUJUGVOVSWLVMVNVSWLPVLVSVMVNOZWLVPVQWOWLPVRWOVPWLWOVPOZBCBLMZVTHVMVNV
    PBWQHIZVMVPVNWRVMVPVNWRPVMVPOVNWRCBUKULRUMUNBUOEZCUOEZVTWQUPWPBCUQVPWSWOBUR
    UGVMWTVNVPCURUSUTVAVBSTVCUBVDVEVOVSWCVMVLVSWCPVNVSVMWCVPVQVMWCPVRVPVMWCWNWA
    WCWIVTVFUHRSTUAUBUIRVOVLKDEZOZWFVSVGVOVLXAWMVHVIZAKBVJUHVOXBWGWDVGXCAKVTVJU
    HVK $.
    $( [14-Sep-2007] $) $( [11-Sep-2007] $)

