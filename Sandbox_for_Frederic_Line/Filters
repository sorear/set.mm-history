$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Filters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Fil $.

  $( Extend class notation with the class of all filters. $)
  cfil $a class Fil $.

  ${
    $d f x y $.
    $( The class of all filters. Bourbaki TG I.36 def. 1 axioms FI, FIIa,
    FIIb, FIII.  This concept is used to define the concept of limit in
    the general case. The notion was invented by Cartan in 1937 and made
    famous by Bourbaki in his treatise. A notion similar to the notion of
    filter is the concept of net invented by Moore and Smith in 1922. $)
    df-fil $a |- Fil = { f | ( ( -. (/) e. f /\ U. f e. f ) /\
      A. x A. y ( ( x e. f /\ y (_ U. f  /\ x (_ y ) -> y e. f ) /\
      A. x e. f A. y e. f ( x i^i y ) e. f ) } $.
  $}

  ${ $d F f x y $. $d X f $.
    isfil.1 $e |- X = U. F $.
    $( The predicate "is a filter." $)
    isfil $p |- ( F e. A -> ( F e. Fil <-> ( ( -. (/) e. F /\ X e. F ) /\
    A. x A. y ( ( x e. F /\ y (_ X /\ x (_ y ) -> y e. F ) /\
    A. x e. F A. y e. F ( x i^i y ) e. F ) ) ) $=
      ( vf c0 cv wcel wn cuni wa wss w3a wi wal cin wral cfil wceq eleq2
      negbid unieq eqcomi syl6eq id eleq12d anbi12d wb sseq2 syl 3anbi12d
      imbi12d 2albidv raleqd 3anbi123d df-fil elab2g ) HGIZJZKZUTLZUTJZMZAIZUTJ
      ZBIZVCNZVFVHNZOZVHUTJZPZBQAQZVFVHRZUTJZBUTSZAUTSZOHDJZKZEDJZMZVFDJZVHENZV
      JOZVHDJZPZBQAQZVODJZBDSZADSZOGDTCUTDUAZVEWBVNWHVRWKWLVBVTVDWAWLVAVSUTDHUB
      UCWLVCEUTDWLVCDLZEUTDUDEWMFUEUFZWLUGUHUIWLVMWGABWLVKWEVLWFWLVGWCVIWDVJUTD
      VFUBWLVCEUAVIWDUJWNVCEVHUKULUMUTDVHUBUNUOVQWJAUTDVPWIBUTDUTDVOUBUPUPUQABG
      URUS $.
      $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  ${
    $d F x y $.
    $( The empty set doesn't belong to a filter. $)
    filesn $p |- ( F e. Fil -> -. (/) e. F ) $=
      ( vx vy cfil wcel c0 wn cuni wa cv wss w3a wi wal cin wral eqid isfil
      ibi 3simp1d pm3.26d ) ADEZFAEGZAHZAEZUBUCUEIZBJZAECJZUDKUGUHKLUHAEMCNBNZU
      GUHOAECAPBAPZUBUFUIUJLBCDAUDUDQRSTUA $.
      $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  ${ $d A x y $. $d B y $. $d F x y $. $d X x y $.
    fillsb.1 $e |- X = U. F $.
    $( A filter is closed under taking supersets. $)
    fillsb $p |- ( F e. Fil -> ( ( A e. F /\
      B (_ X /\ A (_ B ) -> B e. F ) ) $=
      ( vx vy cfil wcel wss w3a wi cvv cuni uniexg eqcomi eleq1i sylib cpw wa
      elpw2g bicomd anbi2d biimpd 3simpa imim1i syl cv wceq eleq1 sseq1
      3anbi13d imbi1d imbi2d sseq2 3anbi23d imbi12d wal c0 wn cin wral isfil
      ibi 3simp2d 19.21bi vtocl2g syl6 com23 pm2.43i pm2.43d ) CHIZACIZBDJZABJZ
      KZBCIZVLVPVPVQLZLZVLDMIZVLVSLVLCNZMIVTCHOWADMDWAEPQRVTVPVLVRVTVPVMBDSZIZT
      ZVLVRLZVTVMVNTZWDLVPWDLVTWFWDVTVNWCVMVTWCVNBDMUAUBUCUDVPWFWDVMVNVOUEUFUGV
      LFUHZCIZGUHZDJZWGWIJZKZWICIZLZLVLVMWJAWIJZKZWMLZLWEFGABCWBWGAUIZWNWQVLWRW
      LWPWMWRWHVMWKWOWJWGACUJWGAWIUKULUMUNWIBUIZWQVRVLWSWPVPWMVQWSWJVNWOVOVMWIB
      DUKWIBAUOUPWIBCUJUQUNVLWNGVLWNGURZFVLUSCIUTDCITZWTFURZWGWIVACIGCVBFCVBZVL
      XAXBXCKFGHCDEVCVDVEVFVFVGVHVIUGVJVK $.
      $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  ${ $d F x y $.
    filusb.1 $e |- X = U. F $.
    $( The underlying set belongs to the filter. $)
    filusb $p |- ( F e. Fil -> X e. F ) $=
      ( vx vy cfil wcel c0 wn wa cv wss w3a wi wal cin wral isfil ibi 3simp1d
      pm3.27d ) AFGZHAGIZBAGZUBUCUDJZDKZAGEKZBLUFUGLMUGAGNEODOZUFUGPAGEAQDAQZUB
      UEUHUIMDEFABCRSTUA $.
      $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  ${ $d A x y $. $d B y $. $d F x y $.
  $( A filter is closed under taking intersections. $)
  filint $p |- ( ( F e. Fil /\ A e. F /\ B e. F ) -> ( A i^i B ) e. F ) $=
    ( vx vy cfil wcel cin wa wi cv wral c0 wn cuni wss w3a wal eqid isfil
    biimpd imp 3simp3d ex wceq wb ineq1 eleq1 syl ineq2 rcla42v com12 syl6
    pm2.43i 3impib ) CFGZACGZBCGZABHZCGZUPUQURIZUTJZUPUPDKZEKZHZCGZECLDCLZVBUPU
    PVGUPUPIMCGNCOZCGIZVCCGVDVHPVCVDPQVDCGJERDRZVGUPUPVIVJVGQZUPUPVKDEFCVHVHSTU
    AUBUCUDVAVGUTVFUTAVDHZCGZDEABCCVCAUEVEVLUEVFVMUFVCAVDUGVEVLCUHUIVDBUEVLUSUE
    VMUTUFVDBAUJVLUSCUHUIUKULUMUNUO $.
    $( [20-Jul-2007] $) $( [20-Jul-2007] $)
  $}

  $( The intersection of two elements of a filter can't be empty. $)
  fipfil $p |- ( F e. Fil -> ( ( A e. F /\ B e. F ) ->
    ( A i^i B ) =/= (/) ) ) $=
    ( cfil wcel wa cin c0 wceq wn wne nelneq filint 3expb filesn adantr sylanc
    ex df-ne syl6ibr ) CDEZACEZBCEZFZABGZHIJZUEHKUAUDUFUECEZHCEJZUFUAUDFUEHCLUA
    UBUCUGABCMNUAUHUDCOPQRUEHST $.
    $( [19-Sep-2007] $) $( [16-Sep-2007] $)

  ${ $d A x y $. $d F x y $.
    $( A filter has the finite intersection property. Bourbaki TG I.36 note
    of def. 1. $)
    fipfil2 $p |- ( F e. Fil ->
    ( ( A (_ F /\ A =/= (/) /\ E. x e. om A ~~ x ) -> |^| A =/= (/) ) ) $=
      ( vy cfil wcel wss c0 wne cv cen wbr com wrex w3a cint wceq wn wi wa cvv
      ssexg wal sseq1 neeq1 breq1 rexbidv 3anbi123d wb inteq eqeq1 negbid syl
      imbi12d cla4gv nelneq cin wral cuni eqid isfil biimpa 3simp3d anidms
      fiint sylib 19.21bi imp filesn adantr sylanc ex 19.21aiv syl5 com23
      com14 pm2.43i com13 3ad2ant1 com12 df-ne syl6ibr ) CEFZBCGZBHIZBAJZKLZAMN
      ZOZBPZHQZRZWJHIWIWCWLWIWCWLSZWDWEWIWMSZWHWCWIWDWLWCWIWDWLSSWDWCWIWCWLWDWC
      WNWDWCTBUAFZWNBCEUBWOWCWIWLWODJZCGZWPHIZWPWFKLZAMNZOZWPPZHQZRZSZDUCWIWLSZ
      WCXEXFDBUAWPBQZXAWIXDWLXGWQWDWRWEWTWHWPBCUDWPBHUEXGWSWGAMWPBWFKUFUGUHXGXB
      WJQZXDWLUIWPBUJXHXCWKXBWJHUKULUMUNUOWCXEDWCXAXDXBCFZHCFRZXDWCXATXBHCUPWCX
      AXIWCXAXISZDWCWPWFUQCFACURDCURZXKDUCWCXLWCWCTXJCUSZCFTZWPCFWFXMGWPWFGOWFC
      FSAUCDUCZXLWCWCXNXOXLODAECXMXMUTVAVBVCVDDACVEVFVGVHWCXJXACVIVJVKVLVMVNVOU
      MVLVPVQVRVSVQVTWJHWAWB $.
      $( [28-Oct-2007] $) $( [20-Sep-2007] $)
  $}

  $( The empty set is not a filter. Bourbaki TG I.36 def 1 note. $)
  emnfil $p |- -. (/) e. Fil $=
    ( c0 cfil wcel cuni noel eqid filusb mto ) ABCADZACIEAIIFGH $.
    $( [31-Oct-2007] $) $( [30-Oct-2007] $)

  ${ $d A x y $. $d B x y $.
    $( A singleton is a filter. Bourbaki TG I.36, example 1. $)
    oefil2 $p |- ( ( A e. B /\ A =/= (/) ) -> { A } e. Fil ) $=
      ( vx vy wcel c0 wne wa csn wn cuni cv wss w3a wi wal cin wral cfil elsni
      eqcomd necon3ai adantl wceq unisng adantr snidg eqeltrd jca sseq2d sseq1
      eqss biimpr ancoms elsn sylibr ex syl6bi com3r com23 syl5ib 3impd
      19.21aivv ineq12 inidm syl6eq visset inex1 elsnc syl2anb rgen2a a1i 3jca
      cvv wb snex eqid isfil ax-mp ) ABEZAFGZHZFAIZEZJZWCKZWCEZHZCLZWCEZDLZWFMZ
      WIWKMZNWKWCEZOZDPCPZWIWKQZWCEZDWCRCWCRZNZWCSEZWBWHWPWSWBWEWGWAWEVTWDAFWDF
      AFATUAUBUCWBWFAWCVTWFAUDWAABUEZUFVTAWCEWAABUGUFUHUIVTWPWAVTWOCDVTWJWLWMWN
      VTWIAUDZWLWMWNOZOWJVTWLXCXDVTWLWKAMZXCXDOVTWFAWKXBUJXCWMXEWNXCWMAWKMZXEWN
      OWIAWKUKXFXEWNXFXEHWKAUDZWNXEXFXGXGXEXFHWKAULUMUNDAUOZUPUQURUSURUTCAUOZVA
      VBVCUFWSWBWRCDWCXCXGWRWJWNXCXGHZWQAUDWRXJWQAAQAWIAWKAVDAVEVFWQAWIWKCVGVHV
      IUPXIXHVJVKVLVMWCVNEXAWTVOAVPCDVNWCWFWFVQVRVSUP $.
      $( [19-Sep-2007] $) $( [16-Sep-2007] $)
  $}

  ${ $d J x y $. $d S x y $. $d X x y $.
    neifil.1 $e |- X = U. J $.
    $( The neighborhoods of a non empty set is a filter.
    Bourbaki TG I.36, example 2. $)
    neifil $p |- ( ( J e. Top /\ S (_ X /\ S =/= (/) ) ->
      ( ( nei ` J ) ` S ) e. Fil ) $=
      ( vx vy ctop wcel wss c0 wne w3a cnei cfv wn cuni wa cv wi wal cin wral
      cfil 0nnei 3adant2 unnei tpnei biimpa eqeltrd 3adant3 jca wceq sseq2
      ssnei2 exp43 adantr com14 syl6bi com23 3impd mpcom 19.21aivv innei
      3expib 3ad2ant1 r19.21aivv 3jca cvv wb fvex eqid isfil ax-mp sylibr ) BGH
      ZACIZAJKZLZJABMNZNZHOZVTPZVTHZQZERZVTHZFRZWBIZWEWGIZLZWGVTHZSZFTETZWEWGUA
      VTHZFVTUBEVTUBZLZVTUCHZVRWDWMWOVRWAWCVOVQWAVPABUDUEVOVPWCVQVOVPQZWBCVTABC
      DUFZVOVPCVTHABCDUGUHUIUJUKVRWLEFVOVPWLVQWBCULZWRWLWSWTWJWRWKWTWFWHWIWRWKS
      ZWTWHWFWIXASZWTWHWGCIZWFXBSWBCWGUMWRWFWIXCWKVOWFWIXCWKSSSVPVOWFWIXCWKABWG
      WECDUNUOUPUQURUSUTUSVAUJVBVRWNEFVTVTVOVPWFWKQWNSVQVOWFWKWNABWGWEVCVDVEVFV
      GVTVHHWQWPVIAVSVJEFVHVTWBWBVKVLVMVN $.
      $( [19-Sep-2007] $) $( [18-Sep-2007] $)
  $}

  ${ $d F x y $. $d G x y $.
  $( The intersection of two filters is a filter. Use  ~ fiint to extend this
  property to the intersection of a finite set of filters. Bourbaki TG I.36
  par. 3. $)
  filintf $p |- ( ( F e. Fil /\ G e. Fil /\ U. F = U. G ) ->
      ( F i^i G ) e. Fil ) $=
    ( vx vy cfil wcel cuni wceq w3a cin wi cvv c0 wn wa cv wss wal wral eqid
    isfil filesn elin pm3.26bi nsyl 3ad2ant1 ineq2 inidm syl5eqr 3ad2ant3
    filusb eleq1 biimpr expcom syl6bir com3l syl syl5com 3imp eqeltrrd elssuni
    uniin jctil eqss sylibr eqeltrd jca fillsb 3ad2ant2 anim12d biimp sstr2
    mpi ssin pm4.24 3anim123i an6 syl5 syl6ibr 19.21aivv filint 3expib
    im2anan9 com12 an4s syl2anb 3adant3 r19.21aivv 3jca syl5cbir inex1g
    pm2.43i ) AEFZBEFZAGZBGZHZIZABJZEFZWMWNWRWTKWQWRWSLFZWTWMXAWTMWSFZNZWSGZWSF
    ZOZCPZWSFZDPZXDQZXGXIQZIZXIWSFZKZDRCRZXGXIJZWSFZDWSSCWSSZIWRCDLWSXDXDTUAWRX
    FXOXRWRXCXEWMWNXCWQWMMAFZXBAUBXBXSMBFMABUCUDUEUFWRXDWOWPJZWSWRXDXTQZXTXDQZO
    XDXTHWRYBYAWRXTWSFYBWRWOXTWSWQWMWOXTHWNWQWOWOJXTWOWOWPWOUGWOUHUIUJWMWNWQWOW
    SFZWNWOAFZWQYCKZWMWNWPBFZYDYEKBWPWPTZUKWQYFYDYCWQYFWOBFZYDYCKWOWPBULYDYHYCY
    CYDYHOWOABUCUMUNUOUPUQAWOWOTZUKURUSUTZXTWSVAUQABVBZVCXDXTVDVEYJVFVGWRXNCDWR
    XLXIAFZXIBFZOZXMWRXGAFZXIWOQZXKIZXGBFZXIWPQZXKIZOZYNXLWRYQYLYTYMWMWNYQYLKWQ
    XGXIAWOYIVHUFWNWMYTYMKWQXGXIBWPYGVHVIVJXLYOYROZYPYSOZXKXKOZIUUAXHUUBXJUUCXK
    UUDXHUUBXGABUCZVKXJXIXTQZUUCXJYAUUFYKXIXDXTVLVMXIWOWPVNVEXKUUDXKVOVKVPYOYPX
    KYRYSXKVQVEVRXIABUCZVSVTWRXQCDWSWSWMWNXHXMOZXQKWQUUHWMWNOZXQUUBYNUUIXQKZXHX
    MYOYLYRYMUUJUUIYOYLOZYRYMOZOZXQUUIUUMXPAFZXPBFZOXQWMUUKUUNWNUULUUOWMYOYLUUN
    XGXIAWAWBWNYRYMUUOXGXIBWAWBWCXPABUCVSWDWEUUEUUGWFWDWGWHWIWJABEWKURUFWL $.
    $( [19-Sep-2007] $) $( [17-Sep-2007] $)
  $}

  ${ $d A u x y $. $d B a b u v x y $. $d B u x y z $. $d X a b x y $.
     $d X x y z $. $d w x y $.
    fgsb.1 $e |- B = { x | E. y
                  ( y (_ A /\ E. z e. om y ~~ z /\ x = |^| y ) } $.
    $( Filter generated by a subbasis ` A ` . Bourbaki TG I.37 paragraph
    above prop. 1. The theorem has been slightly modified because
    the definitions of the empty set are different in Bourbaki and Metamath.
    ) $)
    fgsb   $p |- ( ( A (_ P~ X /\ X e. V /\ A =/= (/) ) -> ( -. (/) e. B ->
      { x e. P~ X | E. y e. B y (_ x } e. Fil ) ) $=
      ( va vb vu vw vv cpw wss cvv wcel c0 wne w3a wn cv wrex crab cfil wa
      cuni wi wal cin wral wceq sseq2 rexbidv elrab ss0 eleq1d biimpcd
      r19.23aiv adantl sylbi con3i ump 3ad2ant2 r19.2z cen wbr com cint wex
      cab fine neeq1i sylibr 3ad2ant3 ax-17 eleq1 sseq1 imbi12d eleq2i fiiu
      chvar uniss 3ad2ant1 sylan9ssr unipw syl6ss visset elpw pm3.27 ssid
      jctir rcla4ev syl jca cbvrexv syl5bb jctil ssuni r19.21aiva sylanc
      adantr hbrab1 hbuni hbpw hbss hbrex hbre1 hbrab hbeq rexbid elrabf
      unissb elpwi mprgbir sspwb mpbi sseli sylbir r19.41v sstr r19.22si
      adantll 3adant2 syl3an1b gen2 a1i inpws1 rcla4e biimp anim12i infi
      eqcomi 3syl ss2in syl2an an4s expcom r19.23aiva com12 imp syl2anb pm3.26
      sseq2d rexbidva rgen2 3jca wb pwexg rabexg eqid isfil mpbird ex ) DFMZNZF
      OPZDQRZSZQEPZTZBUAZAUAZNZBEUBZAUUNUCZUDPZUURUUTUEZUVFQUVEPZTZUVEUFZUVEPZU
      EZHUAZUVEPZIUAZUVJNZUVMUVONZSZUVOUVEPZUGZIUHHUHZUVMUVOUIZUVEPZIUVEUJHUVEU
      JZSZUVGUVLUWAUWDUVGUVIUVKUUTUVIUURUVHUUSUVHQUUNPZUVAQNZBEUBZUEUUSUVDUWHAQ
      UUNUVBQUKUVCUWGBEUVBQUVAULUMUNUWHUUSUWFUWGUUSBEUWGUVAEPZUUSUWGUVAQEUVAUOU
      PUQURUSUTVAUSUVGUVJUUNPZUVAUVJNZBEUBZUEZUVKUURUWMUUTUURUWJUWLUUPUUOUWJUUQ
      UVDAFOVBVCEQRZUWKBEUJUWLUURUWKBEVDUUQUUOUWNUUPUUQUVADNUVACUAZVEVFCVGUBUVB
      UVAVHUKSBVIAVJZQRUWNABCDVKEUWPQGVLVMVNUURUWKBEUURUWIUEZUVAUVANZUVAUVEPZUE
      UWKUWQUWSUWRUWQUVAUUNPZUWOUVANZCEUBZUEUWSUWQUWTUXBUWQUVAFNUWTUWQUVAUUNUFZ
      FUWIUURUVADUFZUXCJUAZEPZUXEUXDNZUGUWIUVAUXDNZUGZJBUXIJVOUXEUVAUKUXFUWIUXG
      UXHUXEUVAEVPUXEUVAUXDVQVRUXFUXEUWPPZUXGEUWPUXEGVSZABCUXEDVTUTWAUUOUUPUXDU
      XCNUUQDUUNWBWCWDFWEWFUVAFBWGWHVMUWQUWIUWRUEUXBUWQUWIUWRUURUWIWIUVAWJZWKUX
      AUWRCUVAEUWOUVAUVAVQWLWMWNUVDUXBAUVAUUNUVBUVAUKZUWOUVBNZCEUBUXBUVDUXMUXNU
      XACEUVBUVAUWOULUMUVCUXNBCEUVAUWOUVBVQWOWPUNVMUXLWQUVAUVAUVEWRWMWSWTWNXAUV
      DUWLACUVJUUNACUVEUVDACUUNXBXCZACFUWOFPAVOXDUWKABEUWIAVOACUVAUVJUWOUVAPAVO
      UXOXEXFUVBUVJUKUVCUWKBEBKCUVBUVJKUAUVBPBVOBCUVEUVDBACUUNUVCBEXGUWOUUNPZBV
      OXHXCXIUVBUVJUVAULXJXKVMWNUWAUVGUVTHIUVRUVOUUNPZUVAUVONZBEUBZUEZUVSUVMUUN
      PZUVAUVMNZBEUBZUEZUVPUVQUXTUVNUYDUVPUVQSUXQUXSUVPUYDUXQUVQUVPUVOUVJMZPUXQ
      UVOUVJIWGWHUYEUUNUVOUVJFNZUYEUUNNUYFUWOFNZCUVECUVEFXLUWOUVEPUXPUVAUWONZBE
      UBZUEUYGUVDUYIAUWOUUNUVBUWOUKUVCUYHBEUVBUWOUVAULUMUNUXPUYGUYIUWOFXMXAUTXN
      UVJFXOXPXQXRVCUYDUVQUXSUVPUYCUVQUXSUYAUYCUVQUEUYBUVQUEZBEUBUXSUYBUVQBEXSU
      YJUXRBEUVAUVMUVOXTYAXRYBYCWNUVDUYCAUVMUUNUVBUVMUKZUVCUYBBEUVBUVMUVAULUMUN
      YDUVDUXSAUVOUUNUVBUVOUKZUVCUXRBEUVBUVOUVAULUMUNVMYEYFUWDUVGUWCHIUVEUVEUYD
      UXTUWCUVNUVSUYDUXTUEUWBUUNPZUVAUWBNZBEUBZUEZUWCUYAUXQUYCUXSUYPUYAUXQUEUYM
      UYCUXSUEUYOUYAUYMUXQUVMUVOFYGXAUXEUVMNZJEUBZLUAZUVONZLEUBZUYOUYCUXSUYRVUA
      UYOUYQVUAUYOUGJEVUAUXFUYQUEZUYOUYTVUBUYOUGLEVUBUYSEPZUYTUEUYOUXFVUCUYQUYT
      UYOUXEUYSUIZEPZVUDUWBNZUYOUXFVUCUEZUYQUYTUEUYNVUFBVUDEVUFBVOUVAVUDUWBVQYH
      VUGUXJUYSUWPPZUEVUDUWPPZVUEUXFUXJVUCVUHUXFUXJUXKYIVUCVUHEUWPUYSGVSYIYJABC
      UXEUYSDYKVUIVUEUWPEVUDEUWPGYLVSYIYMUXEUVMUYSUVOYNYOYPYQYRYSYRYTUYBUYQBJEU
      VAUXEUVMVQWOUXRUYTBLEUVAUYSUVOVQWOUUAYJYPUVDUYOAUWBUUNUVBUWBUKZUVCUYNBEVU
      JUWIUEUVBUWBUVAVUJUWIUUBUUCUUDUNVMUVDUYCAUVMUUNUYKUVCUYBBEUYKUWIUEUVBUVMU
      VAUYKUWIUUBUUCUUDUNUVDUXSAUVOUUNUYLUVCUXRBEUYLUWIUEUVBUVOUVAUYLUWIUUBUUCU
      UDUNUUAUUEYFUUFUVGUVEOPZUVFUWEUUGUURVUKUUTUUPUUOVUKUUQUUPUUNOPVUKFOUUHUVD
      AUUNOUUIWMVCXAHIOUVEUVJUVJUUJUUKWMUULUUM $.
      $( [30-Nov-2007] $) $( [16-Nov-2007] $)
  $}

  ${ $d A x y z $. $d B u x y z $. $d X u x y z $.
    efilcp.1 $e |- B = { z | E. y
                  ( y (_ A /\ E. u e. om y ~~ u /\ z = |^| y ) } $.
    $( A filter containing a set ` A ` exists iff ` A ` has the finite
    intersection property (i.e. no finite intersection of elements of A
    is empty). Bourbaki TG I.37 prop. 1. $)
    efilcp  $p |- ( ( A (_ P~ X /\ X e. V /\ A =/= (/) ) ->
      ( -. (/) e. B <-> E. x e. Fil A (_ x ) ) $=
      ( cpw wss cvv wcel c0 wne w3a wn cv cfil wrex crab wa sseq2 rcla4ev fgsb
      imp wral 3simp1 adantr cen wbr com cint wceq wex cab abfi sseqtr4 ssel2
      ssid sseq1 mpan2 syl mpan rgen jctir ssrab sylibr sylanc ex wi sstr
      df-ne fipfil2 pm2.27 necom biimp sylbir syl6 3exp com34 com4t com14
      com13 com23 imp31 inteq int0 eqeq1i 0ex ne0i ax-mp neeq1 mpbii sylbi
      eqcoms necomd sylib a1d pm2.61d2 nan mpbir df-3an negbii nexdv eqeq1
      3anbi3d exbidv elab2 exp31 r19.23aiv com12 impbid ) EGIZJZGKLZEMNZOZMFLZP
      ZEAQZJZARSZXQXSYBBQZCQZJZBFSZCXMTZRLZEYGJZYBXQXSUAZYAYIAYGRXTYGEUBUCXQXSY
      HCBDEFGHUDUEYJXNYFCEUFZUAYIYJXNYKXQXNXSXNXOXPUGUHYFCEEFJZYDELZYFEYCEJZYCD
      QUIUJDUKSZYDYCULZUMZOZBUNZCUOFCBDEUPHUQYLYMUAYDFLZYFEFYDURYTYDYDJZYFYDUSY
      EUUABYDFYCYDYDUTUCVAVBVCVDVEYFCXMEVFVGVHVIYBXQXSYAXQXSVJARXTRLZYAXQXSUUBY
      AUAXQUAZYNYOMYPUMZOZBUNZPXSUUCUUEBUUCYNYOUAZUUDUAZPZUUEPUUCUUIVJUUCUUGUAU
      UDPZVJUUCUUGUUJUUCYCMUMZUUGUUJVJZUUBYAXQUUKPZUULVJZUUBXQYAUUNUUBXQYAUUNVJ
      UUGYAUUMUUBXQUAZUUJYNYOYAUUMUUOUUJVJZVJZVJYNYAYOUUQYNYAYOUUQVJZYNYAUAYCXT
      JZUURYCEXTVKUUMYOUUSUUPUUMYCMNZYOUUSUUPVJVJYCMVLUUOYOUUSUUTUUJUUBYOUUSUUT
      UUJVJVJVJZXQUUBUUSUUTYOOZYPMNZVJZUVADYCXTVMUUSUUTUVDYOUUJUUSUUTYOUVDUUJUU
      SUUTYOUVDUUJVJUVBUVDUVCUUJUVBUVCVNUVCMYPNZUUJMYPVOUVEUUJMYPVLZVPVQVRVSVTW
      AVBUHWBVQWCVBVIWDUEWBVIWDWEUUKUUJUUGUUKUVEUUJUUKYPMUVCMYCMYCUMMULZYPUMZUV
      CMYCWFUVHKYPUMZUVCUVGKYPWGWHUVIKMNZUVCMKLUVJWIKMWJWKKYPMWLWMWNVBWOWPUVFWQ
      WRWSUEUUCUUGUUDWTXAUUEUUHYNYOUUDXBXCVGXDXRUUFYSUUFCMFWIYDMUMZYRUUEBUVKYQU
      UDYNYOYDMYPXEXFXGHXHXCVGXIXJXKXL $.
      $( [30-Nov-2007] $) $( [20-Nov-2007] $)
  $}

