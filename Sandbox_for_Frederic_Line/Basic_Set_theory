$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Basic Set theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y z $. $d B x y z $. $( ` z ` is dummy. $)
    $( The intersection of a union ` U. A ` with a set ` B ` is equal to the
       union of the intersections of each element of ` A ` with ` B ` . $)
    ntunte $p |- ( U. A i^i B ) = U. { x | E. y e. A x = ( y i^i B ) } $=
      ( vz cuni cin cv wceq wrex cab wcel wa wex eluni2 anbi1i elin ancom
      r19.41v bitr4 exbii rexcom4 visset inex1 eleq2 ceqsexv bitr rexbii 3bitr
      3bitr4 eluniab eqriv ) ECFZDGZAHZBHZDGZIZBCJZAKFZEHZUNLZVAUOLZUSMZANZVAUT
      LVAUMLZVADLZMVAUPLZBCJZVGMZVBVEVFVIVGBVACOPVAUMDQVEURVCMZANZBCJZVHVGMZBCJ
      VJVEVKBCJZANVMVDVOAVDUSVCMVOVCUSRURVCBCSTUAVKBACUBTVLVNBCVLVAUQLZVNVCVPAU
      QUPDBUCUDUOUQVAUEUFVAUPDQUGUHVHVGBCSUIUJUSAVAUKTUL $.
      $( [24-Mar-2007] $) $( [24-Mar-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.  $d x y z S $.
    $d x ps $.  $d x y ch $.  $d x y z th $.
    oprabvaligg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    oprabvaligg.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    oprabvaligg.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    oprabvaligg.4 $e |- E* z ph $.
    oprabvaligg.5 $e |- F =
                  { <. <. x , y >. , z >. | ph } $.
    $( The value of an operation class abstraction (weak version). $)
    oprabvaligg $p |- ( ( A e. R /\ B e. S /\ C e. D ) ->
                         ( th -> ( A F B ) = C ) ) $=
      ( wcel w3a cop copab2 cfv wceq co eloprabg wi wfun funoprab funopfvg
      mpan2 3ad2ant3 sylbird df-opr fveq1i eqtr eqeq1i syl6ibr ) HLTZIMTZJKTZUA
      ZDHIUBZAEFGUCZUDZJUEZHINUFZJUEVCDVDJUBVETZVGABCDEFGHIJLMKOPQUGVBUTVIVGUHZ
      VAVBVEUIVJAEFGRUJVDJKVEUKULUMUNVHVFJVHVDNUDVFHINUOVDNVESUPUQURUS $.
      $( [14-Sep-1999] $)
  $}

  ${
    $d a b c x u v $. $d a b x C $. $d a b c x B $. $d a b x u v A $.
    $d R a b u v x $. $d C a b c x $.
    uninqs.1 $e |- Er R $.
    $( The class union of the intersection of two subclasses of a
       quotient space.  Compare ~ uniin . $)
    uninqs  $p |- ( ( B (_ ( A /. R ) /\ C (_ ( A /. R ) ) ->
      U. ( B i^i C ) = ( U. B i^i U. C ) ) $=
      ( vx vc vb va vu vv cqs wss wa cin cuni wceq cv wcel wex wi wal dfss2
      pm3.35 cec visset elqs c0 wne inelcm wn df-ne wo erdisj pm5.61 eqeq12
      biimprcd adantr sylbi mpan syl eleq2 biimpa syl2an an4s exp32 com24
      pm2.43i ex adantl 19.23aiv com3l com23 imp com13 imp31 eleq1 elin elequ2
      biimprd wb incom eleq2i syl6bb equcoms biimpd anim12d a4w expcom sylbir
      syl6bi syl9 com3r pm2.43a com14 com4t a4s com12 eluni syl7ib syl5ib
      imp3a 3imtr4g ssrdv uniin jctil eqss sylibr ) BADLZMZCXIMZNZBCOZPZBPZCPZO
      ZMZXQXNMZNXNXQQXLXSXRXLFXQXNXLFRZXOSZXTXPSZNXTGRZSZYCXMSZNZGTZXTXQSXTXNSX
      LYAYBYGXLXTHRZSZYHBSZNZHTZYBYGUAYAXLYLXTIRZSZYMCSZNZITZYGYBYLXLYQYGUAZYKX
      LYRUAHYQXLYKYGYPXLYKYGUAZUAIXLYPYSXJXKYPYSUAZXJYJYHXISZUAZHUBXKYTUAZHBXIU
      CUUBUUCHXKUUBYTXKYOYMXISZUAZIUBUUBYTUAZICXIUCUUEUUFIYPYKUUEUUBYGYNYOYKUUE
      UUBYGUAZUAZUAYKYOYNUUHYIYJYOYNUUHUAZUAZYIYJUUJUAYJYIYIUUJYIYJYIUUJUAZYJYI
      YJUUKUAYOYJYIYJYINZUUIYJYOYIUULUUIUAZUAZYOYJYOUUNUAYOYJNZYIYOUUMUUOYIYOUU
      MUAYOUULUUOYINZUUIYOUULUUPUUIUAYOUULNZYNUUPUUHUUQYNUUPUUHUAUUQYNNZUUEUUPU
      UGUURUUEUUPUUGUAUURUUENUUBYMYHQZUUPYGUUQYNUUEUUBUUSUAUUQUUBUUEYNUUSYOUULU
      UBUUEYNUUSUAZUAUAYOUUEUUBUULUUTYOUUEUUBUULUUTUAUAZYOUUENUUDUVAYOUUDUDUULU
      UBUUDUUTYJYIUUBUUDUUTUAZUAYJUUBYIUVBYJUUBYIUVBUAZYJUUBNUUAUVCYJUUAUDUUAJR
      ZASZYHUVDDUEZQZNZJTUVCJAYHDHUFUGUVHUVCJUVGUVCUVEUUDUVGYIUUTUUDKRZASZYMUVI
      DUEZQZNZKTUVGYIUUTUAZUAZKAYMDIUFUGUVMUVOKUVLUVOUVJUVLUVGUVNUVLUVGNZUVNUVP
      YNYIUVPUUSUVPYNYIUVPUUSUAZUVLYNUVGYIUVQXTUVKSZXTUVFSZUVQUVLYNNUVGYINUVRUV
      SNUVKUVFOZUHUIZUVQXTUVKUVFUJUWAUVTUHQZUKZUVQUVTUHULUVKUVFQZUWBUMZUWCUVQUV
      IUVDDKUFJUFEUNUWEUWCNUWDUWCNUVQUWDUWBUOUWDUVQUWCUVPUUSUWDYMUVKYHUVFUPUQUR
      USUTUSVAUVLYNUVRYMUVKXTVBVCUVGYIUVSYHUVFXTVBVCVDVEVFVGVHVIVJVKUSVLVJVKUSV
      AVIVMVNVOVAVIVGVNVGVPUUOYIUUSYGUAUUOUUSYIYGYOYJUUSYIYGUAZUAUUSYOYJUWFUUSY
      OYHCSZYJUWFUAYMYHCVQUWGYJUWFUWGYJNYHCBOZSZUWFYHCBVRYIUWIYGYIUWINYFGHYCYHQ
      ZYIYDUWIYEUWJYDYIGHFVSVTUWJUWIYEUWIYEWAHGYHYCQUWIYCUWHSYEYHYCUWHVQUWHXMYC
      CBWBWCWDWEWFWGWHWIWJVIWKVLVNVMVNWLVIVMVIVMVIWMVIVMVIWNWOVIWNVOVHVNVOVNWPW
      QUSWRWQUSVNWRVKVOVKWRIXTCWSWTHXTBWSXAXBXTXOXPVRGXTXMWSXCXDBCXEXFXNXQXGXH
      $.
      $( [4-Jun-2007] $) $( [25-May-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.
    erdisj2.1 $e |- A e. V $.
    erdisj2.2 $e |- B e. V $.
    $( Equivalence classes do not overlap. $)
    erdisj2 $p |- ( Er R -> ( [ A ] R = [ B ] R \/
      ( [ A ] R i^i [ B ] R ) = (/) ) ) $=
      ( wer cec wceq cin c0 wo cid cif eceq1 eqeq12d ineq12d eqeq1d orbi12d
      ereq ider elimhyp erdisj dedth ) CFZACGZBCGZHZUEUFIZJHZKAUDCLMZGZBUJGZHZU
      KULIZJHZKCLCUJHZUGUMUIUOUPUEUKUFULCUJANZCUJBNZOUPUHUNJUPUEUKUFULUQURPQRAB
      UJDEUDUJFLFCLCUJSLUJSTUAUBUC $.
      $( [4-Jun-2007] $) $( [1-Jun-2007] $)
  $}

  ${
    $d A x $. $d B x $. $d C x $.
    $( Inference from membership to difference. $)
    difeqri2 $p |- ( A. x ( ( x e. A /\ -. x e. B ) <-> x e. C )
      -> ( A \ B ) = C ) $=
      ( cv wcel wn wa wb wal cab cdif wceq bicom albii biimp abeq2 sylibr
      df-dif syl6reqr ) AEZBFUACFGHZUADFZIZAJZDUBAKZBCLUEUCUBIZAJZDUFMUEUHUDUGA
      UBUCNOPUBADQRABCST $.
      $( [18-Jun-2007] $) $( [6-Jun-2007] $)
  $}

  ${ $d A u v w y z $. $d B u v w y z $. $d C u v w y z $. $d D u v w y z $.
     $d ph u x $. $d ta u v w y z $. $d v w x y z $.
    elo.1 $e |-   ( y = A -> ( ph <-> ps ) ) $.
    elo.2 $e |-   ( z = B -> ( ps <-> ch ) ) $.
    elo.3 $e |-   ( v = C -> ( ch <-> th ) ) $.
    elo.4 $e |-   ( w = D -> ( th <-> ta ) ) $.
    $( See ~ eloprabg . Note: to be used with categories. $)
    elo $p |- ( ( ( A e. Q /\ B e. R /\ C e. S ) /\ D e. T ) ->
      ( <. <. A , B >. , <. C , D >. >. e. { x  | E. y E. z E. v E. w
      ( x = <. <. y , z >. , <. v , w >. >. /\ ph ) } <-> ta ) ) $=
      ( vu cvv wcel w3a cop cv wceq wa wex cab wb wi opex eqeq1 eqcom syl6bb
      cxp visset otthg 3adant3 opthg 3anbi3d and4as 3ad2ant3 bitrd 3simp2
      adantr opelxpi 3ad2antl3 pm3.27 syl3anc sylan9bbr anbi1d syl3an9b
      sylan9bb pm5.32i 4exbidv eleq1 elab syl5bbr adantl elex 3anim123i
      anim12i eeeeanv sylibr biantrurd 19.41vvvv syl6rbbr 3bitr3d expcom
      vtocle elisset syl2an ) KUDUEZLUDUEZMUDUEZUFZNUDUEZKLUGZMNUGZUGZFUHZGUHZH
      UHZUGJUHZIUHZUGZUGZUIZAUJZIUKJUKHUKGUKZFULZUEZEUMZKOUEZLPUEZMQUEZUFNRUEWT
      XAUJZXQUNUCXDXBXCUOYAUCUHZXDUIZXQYAYCUJZYBXKUIZAUJZIUKJUKHUKGUKZXFKUIZXGL
      UIZXHMUIZUFZXINUIZUJZEUJZIUKJUKHUKGUKZXPEYDYFYNGHJIYDYFYMAUJYNYDYEYMAYCYE
      XKXDUIZYAYMYCYEXDXKUIYPYBXDXKUPXDXKUQURWRXCUDUDUSZUEZXAYPYMUMYAWRYRXAUFYP
      YHYIXJXCUIZUFZYMWRYRYPYTUMXAXFXGKLXJXCUDYQGUTHUTXHXIUOVAVBXAWRYTYMUMYRXAY
      TYHYIYJYLUJZUFYMXAYSUUAYHYIXHXIMNUDJUTIUTVCVDYHYIYJYLVEURVFVGWTWRXAWQWRWS
      VHVIWSWQXAYRWRMNUDUDVJVKWTXAVLVMVNVOYMAEYKADYLEYHABYICYJDSTUAVPUBVQVRURVS
      YCYGXPUMYAYCYBXOUEXPYGYBXDXOVTXNYGFYBUCUTXEYBUIZXMYFGHJIUUBXLYEAXEYBXKUPV
      OVSWAWBWCYAYOEUMYCYAEYMIUKJUKHUKGUKZEUJYOYAUUCEYAYHGUKZYIHUKZYJJUKZUFZYLI
      UKZUJUUCWTUUGXAUUHWQUUDWRUUEWSUUFGKUDWDHLUDWDJMUDWDWEINUDWDWFYHYIYJYLHJIG
      WGWHWIYMEHJIGWJWKVIWLWMWNXRWQXSWRXTWSKOWOLPWOMQWOWENRWOWP $.
      $( [14-Jul-2007] $) $( [14-Jul-2007] $)
  $}

  ${ $d A x y $. $d C x $. $d x z $.
  $( Specific properties of a finite intersection. $)
  spfi $p |- ( A e. B -> ( A e. { x | E. y ( y (_ C /\ E. z e. om y ~~ z /\
       x = |^| y ) }
   <-> E. y ( y (_ C /\ E. z e. om y ~~ z /\ A = |^| y ) ) ) $=
    ( cv wss cen wbr com wrex cint wceq w3a wex eqeq1 3anbi3d exbidv elabg ) BG
    ZFHZUACGIJCKLZAGZUAMZNZOZBPUBUCDUENZOZBPADEUDDNZUGUIBUJUFUHUBUCUDDUEQRST $.
    $( [31-Oct-2007] $) $( [30-Oct-2007] $)
  $}

  ${ $d A a b y $. $d A x y $. $d B a b y $. $d B x y $.
     $d C a b y $. $d C x y $. $d a b y z $. $d x y z $.
  $( The intersection of two finite intersections is a finite intersection. $)
  infi $p |- ( ( A e. { x | E. y ( y (_ C /\ E. z e. om y ~~ z /\ x = |^| y ) }
   /\ B e. { x | E. y ( y (_ C /\ E. z e. om y ~~ z /\ x = |^| y ) } ) ->
  ( A i^i B ) e. { x | E. y ( y (_ C /\ E. z e. om y ~~ z /\ x = |^| y ) } ) $=
    ( va vb cv wss cen wbr com wrex cint wceq w3a wex cab wcel wa cin cvv
    inex1g adantr wi spfi cun sseq1 breq1 rexbidv inteq eqeq2d 3anbi123d
    cla4egv visset unex a1i an6 unss biimp unfi ineq2 ineq1 adantl incom intun
    eqtr4 3eqtrd 3anim123i sylbi sylc expcom 19.23aiv com12 imp cbvexv syl2anb
    3adant3 wb bicomd 3ad2ant3 mpbid 3exp syl6bi imp31 an4s ex mpid pm2.43i )
    DBIZFJZWKCIZKLZCMNZAIWKOZPQBRASZTZEWQTZUAZDEUBZWQTZWTWTXAUCTZXBWRXCWSDEWQUD
    UEWTWTXCXBUFZWRWRWSWSXDWRWRWSWSUAZXDWRWRWLWODWPPZQZBRZXEXDUFABCDWQFUGXEXHXD
    WSWSXHXDUFZWSWSWLWOEWPPZQZBRZXIABCEWQFUGXLXHXCXBXLXHXCQWLWOXAWPPZQZBRZXBXLX
    HXOXCGIZFJZXPWMKLZCMNZEXPOZPZQZGRZHIZFJZYDWMKLZCMNZDYDOZPZQZHRZXOXLXHYCYKXO
    YBYKXOUFGYKYBXOYJYBXOUFHYBYJXOXPYDUHZUCTZYLFJZYLWMKLZCMNZXAYLOZPZQZXOYBYJUA
    ZXNYSBYLUCWKYLPZWLYNWOYPXMYRWKYLFUIUUAWNYOCMWKYLWMKUJUKUUAWPYQXAWKYLULUMUNU
    OYMYTXPYDGUPHUPUQURYTXQYEUAZXSYGUAZYAYIUAZQYSXQXSYAYEYGYIUSUUBYNUUCYPUUDYRU
    UBYNXPYDFUTVACXPYDVBUUDXADXTUBZYHXTUBZYQYAXAUUEPYIEXTDVCUEYIUUEUUFPYADYHXTV
    DVEUUFYQPUUDUUFXTYHUBYQYHXTVFXPYDVGVHURVIVJVKVLVMVNVOVNVPXKYBBGWKXPPZWLXQWO
    XSXJYAWKXPFUIUUGWNXRCMWKXPWMKUJUKUUGWPXTEWKXPULUMUNVQXGYJBHWKYDPZWLYEWOYGXF
    YIWKYDFUIUUHWNYFCMWKYDWMKUJUKUUHWPYHDWKYDULUMUNVQVRVSXCXLXOXBVTXHXCXBXOABCX
    AUCFUGWAWBWCWDWEVPVOWEWFWGWHWIWJ $.
    $( [29-Nov-2007] $) $( [31-Oct-2007] $)
  $}

  ${ $d A a x y $. $d a y z $.
    $( Condition to have a non empty finite intersections class. $)
    fine $p |- ( A =/= (/) ->
     { x | E. y ( y (_ A /\ E. z e. om y ~~ z /\ x = |^| y ) } =/= (/) ) $=
      ( va cv wcel wex wss cen wbr com wrex cint wceq w3a c0 wne cab ax-17 csn
      wa cvv wi snex sseq1 breq1 rexbidv inteq eqeq2d 3anbi123d cla4egv ax-mp
      snssi adantl snfi a1i visset intsn eqcomi eqeq2i biimp adantr syl3anc ex
      a4c 19.23aiv ne0 abn0 3imtr4 ) EFZDGZEHBFZDIZVMCFZJKZCLMZAFZVMNZOZPZBHZAH
      ZDQRWBASQRVLWCEVLWBAEVLATVRVKOZVLWBVKUAZDIZWEVOJKZCLMZVRWENZOZWBWDVLUBZWE
      UCGWFWHWJPZWBUDVKUEWAWLBWEUCVMWEOZVNWFVQWHVTWJVMWEDUFWMVPWGCLVMWEVOJUGUHW
      MVSWIVRVMWEUIUJUKULUMVLWFWDVKDUNUOWHWKCVKUPUQWDWJVLWDWJVKWIVRWIVKVKEURUSU
      TVAVBVCVDVEVFVGEDVHWBAVIVJ $.
      $( [29-Nov-2007] $) $( [16-Nov-2007] $)
  $}

  ${ $d A x y  $. $d x y z $.
    $( Any element of ` A ` is the intersection of a finite subclass of
       ` A ` . $)
  abfi $p |- A (_ { x | E. y ( y (_ A /\ E. z e. om y ~~ z /\ x = |^| y ) } $=
    ( cv wss cen wbr com wrex cint wceq w3a wex cab wcel wi ssab csn cpw wa
    snfi visset intsn eqcomi breq1 rexbidv inteq eqeq2d anbi12d rcla4ev ex
    mp2ani snelpw df-rex elpw anbi1i 3anass bitr4 exbii bitr2 3imtr4 mpgbir )
    DBEZDFZVDCEZGHZCIJZAEZVDKZLZMZBNZAOFVIDPZVMQAVMADRVISZDTZPZVHVKUAZBVPJZVNVM
    VQVOVFGHZCIJZVIVOKZLZVSCVIUBWBVIVIAUCZUDUEVQWAWCUAZVSVRWEBVOVPVDVOLZVHWAVKW
    CWFVGVTCIVDVOVFGUFUGWFVJWBVIVDVOUHUIUJUKULUMVIDWDUNVSVDVPPZVRUAZBNVMVRBVPUO
    WHVLBWHVEVRUAVLWGVEVRVDDBUCUPUQVEVHVKURUSUTVAVBVC $.
    $( [29-Nov-2007] $) $( [18-Nov-2007] $)
  $}

  ${ $d A x y $. $d B x y $. $d x z $.
    $( If ` A ` is the intersection of a finite set of elements of ` B ` then
       ` A (_ U. B ` . $)
    fiiu $p |- ( A e.
        { x | E. y ( y (_ B /\ E. z e. om y ~~ z /\ x = |^| y ) }
       -> A (_ U. B ) $=
      ( cv wss cen wbr com wrex cint wceq w3a wex cab wcel cuni cvv wn nvel
      eleq1 biimpcd mtoi wi spfi eqeq1 negbid biimpd c0 int0 eqeq2 bicomd
      inteq con3i sseq1 eqcoms wne intssuni2 df-ne sylan2br ancoms 3adant2
      syl5com 3exp com24 syl syl6bi ax-mp syl6com com13 pm2.43i com4t 3imp
      19.23aiv mpid ) DBFZEGZVQCFHICJKZAFVQLZMNBOAPZQZDERZGZWBWBDSMZTZWDWBWESWA
      QZWAUAWEWBWGDSWAUBUCUDWBWBVRVSDVTMZNZBOWFWDUEZABCDWAEUFWIWJBVRVSWHWJWHWFV
      RVSWDWHWFVRVSWDUEUEZUEWFWHWHWKWHWFVTSMZTZWHWKUEZWHWFWMWHWEWLDVTSUGUHUIUJL
      ZSMZWMWNUEUKWPWMVTWOMZTZWNWPWLWQWPWQWLWOSVTULUMUHWRVQUJMZTZWNWSWQVQUJUNUO
      WTVSVRWHWDWTVSVRWHWDUEWHVTWCGZWDWTVSVRNXAWDUEVTDVTDMXAWDVTDWCUPUIUQWTVRXA
      VSVRWTXAVRVQUJURXAWTVQEUSVQUJUTVAVBVCVDVEVFVGVHVIVJVKVLVMVNVOVHVPVL $.
      $( [30-Nov-2007] $) $( [30-Nov-2007] $)
  $}

  $( An intersection with a member of a powerset belongs to this powerset. $)
  inpws1 $p |- ( A e. P~ C -> ( A i^i B ) e. P~ C ) $=
    ( cin cvv wcel cpw inex1g wss elpwg elpwi ssinss1 syl syl5bir mpcom ) ABDZE
    FZACGZFZPRFZABRHQTPCIZSPCEJSACIUAACKABCLMNO $.
    $( [28-Oct-2007] $) $( [25-Sep-2007] $)

  $( An intersection with a member of a powerset belongs to this powerset. $)
  inpws2 $p |- ( B e. P~ C -> ( A i^i B ) e. P~ C ) $=
    ( cpw wcel cin inpws1 incom syl5eqel ) BCDZEBAFJABFBACGABHI $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)

  ${
    $d v x $. $d w x $.
    $d x y $. $d x z $.
  $( Structure of the class abstraction used by ` Alg ` , ` Cat ` and
     ` Ded ` . $)
  stcat $p |- { x | E. y E. z E. v E. w ( x = <. <. y , z >. , <. v , w >. >.
       /\ ph ) } (_ ( ( V X. V ) X. ( V X. V ) ) $=
    ( cv cop wceq wa wex cvv cxp wcel opex opelxp visset mpbir2an eleq1 mpbiri
    adantr 19.23aivv abssi ) BGZCGZDGZHZFGZEGZHZHZIZAJZEKFKZDKCKBLLMZUOMZUNUDUP
    NZCDUMUQFEULUQAULUQUKUPNZURUGUONZUJUONZUGUJUOUOUHUIOPUSUELNUFLNUEUFLLDQZPCQ
    VARUTUHLNUILNUHUILLEQZPFQVBRRUDUKUPSTUAUBUBUC $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)
  $}

  $( A theorem of the ~ 1st2nd family. $)
  11st22nd $p |- ( ( ( Rel B /\ Rel dom B /\ Rel ran B ) /\ A e. B ) ->
     A = <. <. ( 1st ` ( 1st ` A ) ) , ( 2nd ` ( 1st ` A ) ) >. ,
            <. ( 1st ` ( 2nd ` A ) ) , ( 2nd ` ( 2nd ` A ) ) >. >. ) $=
    ( wrel cdm crn w3a wcel wa c1st cfv c2nd cop wceq 1st2nd 3ad2antl1 wi
    1stdm sylan2 exp32 a1i com13 3imp1 2ndrn com3r opeq12d eqtrd ) BCZBDZCZBEZC
    ZFABGZHZAAIJZAKJZLZUNIJUNKJLZUOIJUOKJLZLUGUIULAUPMUKABNOUMUNUQUOURUGUIUKULU
    NUQMZUKUIUGULUSPZUIUGUTPPUKUIUGULUSUIUNUHGUSUGULHZUNUHNABQRSTUAUBUGUIUKULUO
    URMZUIUKUGULVBPZUKUGVCPPUIUKUGULVBUKUOUJGVBVAUOUJNABUCRSTUDUBUEUF $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)

  ${ $d A x $.
    $( The union of a part of a powerset belongs to it. $)
    ump $p |- ( A e. B -> U. { x e. P~ A | ph } e. P~ A ) $=
      ( wcel cpw crab cuni wss ssrab2 sspwuni mpbi elpw2g mpbiri ) CDEABCFZGZHZ
      OEQCIZPOIRABOJPCKLQCDMN $.
      $( [29-Nov-2007] $) $( [16-Nov-2007] $)
  $}

  ${
    boe.1 $e |- A e. B $.
    $( ` { A , A } ` has only one element. $)
    boe $p |- { A , A } ~~ 1o $=
      ( csn cpr c1o cen dfsn2 elisseti ensn1 eqbrtrr ) ADAAEFGAHAABCIJK $.
      $( [29-Nov-2007] $) $( [29-Nov-2007] $)
  $}

  $( Moving an element out from the intersection of a class. $)
  moec $p |- ( B e. A -> |^| A = ( B i^i |^| ( A \ { B } ) ) ) $=
    ( wcel cint csn cdif cun cin difsnid eqcomd inteqd wceq intun a1i cvv
    elisset c0 cif sneq id eqeq12d 0ex elimel intsn dedth syl ineq2d incom
    syl6eq 3eqtrd ) BACZADABEZFZULGZDZUMDZULDZHZBUPHZUKAUNUKUNAABIJKUOURLUKUMUL
    MNUKURUPBHUSUKUQBUPUKBOCZUQBLZBAPUTVAUTBQRZEZDZVBLBQBVBLZUQVDBVBVEULVCBVBSK
    VETUAVBBQOUBUCUDUEUFUGUPBUHUIUJ $.
    $( [29-Nov-2007] $) $( [29-Nov-2007] $)

 ${ $d A z $. $d B z $. $d x y $. $d x z $.
  $( Bound-variable hypothesis builder for the "maps to" notation. $)
  hbcmpt $p |- ( y e. ( x e. A |-> B ) -> A. x y e. ( x e. A |-> B ) ) $=
    ( vz cmpt cv wcel wceq wa copab df-mpt hbopab1 hbxfr ) ABACDFAGCHEGDIJZAEKA
    ECDLOAEBMN $.
    $( [17-Feb-2008] $) $( [17-Feb-2008] $)
 $}

 ${ $d A y $. $d B y $. $d x y $.
  $( Value of a function given by the "maps to" notation. $)
  fvopab2a $p |- ( ( x e. A /\ B e. C ) ->
                   ( ( x e. A |-> B ) ` x ) = B ) $=
    ( vy cv wcel wa wceq copab cfv cmpt fvopab2 df-mpt fveq1i syl5eq ) AFZBGZCD
    GHQREFCIHAEJZKCQABCLZKAEBCDMQTSAEBCNOP $.
    $( [17-Feb-2008] $) $( [17-Feb-2008] $)
 $}

 ${ $d A w x y z $. $d C w x y z $.
   cmpbva.1 $e |- F = ( x e. A |-> C ) $.
   $( Rule to change a bound variable int the "maps to" notation. $)
   cmpbva $p |- F = ( y e. A |-> C ) $=
     ( vz vw cv wcel wceq wa copab cmpt df-mpt eleq1 eqeq1 bi2anan9 cbvopabv
     3eqtr eqtr4 ) EBIZCJZGIZDKZLZBGMZBCDNEACDNAIZCJZHIZDKZLZAHMUGFAHCDOULUFAHB
     GUHUBKUIUCUJUDKUKUEUHUBCPUJUDDQRSTBGCDOUA $.
     $( [17-Feb-2008] $) $( [17-Feb-2008] $)
 $}

 ${ $d A x y $. $d C y $.
   fopab2ga.1 $e |- F = ( x e. A |-> C ) $.
   $( Functionality and domain of a class given by the "maps to" notation. $)
   fopab2ga $p |- ( A. x e. A C e. V <-> F Fn A ) $=
     ( vy cmpt cv wcel wceq wa copab df-mpt eqtr fnopab2g ) AFBCDDABCGAHBIFHCJK
     AFLEAFBCMNO $.
     $( [17-Feb-2008] $) $( [17-Feb-2008] $)
 $}

 ${ $d A x y  $. $d B x y  $. $d C y $.
   fopab2a.1 $e |- F = ( x e. A |-> C ) $.
   $( Functionality of a class given by the "maps to" notation. $)
   fopab2a $p |- ( A. x e. A C e. B <-> F : A --> B ) $=
     ( vy cmpt cv wcel wceq wa copab df-mpt eqtr fopab2 ) AGBCDEEABDHAIBJGIDKLA
     GMFAGBDNOP $.
     $( [17-Feb-2008] $) $( [17-Feb-2008] $)
 $}

 ${ $d A y $. $d B y $. $d x y $.
   cmp.1 $e |- F = ( x e. A |-> B ) $.
   $( Functionality of a class given by a "maps to" notation. $)
   cmpfun $p |- Fun F $=
     ( vy cmpt wfun cv wcel wceq wa copab wss pm3.27 ssopab2i funopabeq funss
     mp2 wb df-mpt funeq ax-mp mpbir bicomd mpbi ) ABCGZHZDHZUHAIBJZFICKZLZAFMZ
     HZUMUKAFMZNUOHUNULUKAFUJUKOPAFCQUMUORSUGUMKUHUNTAFBCUAUGUMUBUCUDDUGKZUHUIT
     EUPUIUHDUGUBUEUCUF $.
     $( [17-Feb-2008] $) $( [17-Feb-2008] $)
 $}

 ${ $d A x y $. $d B y $.
   cmpdom.1 $e |- F = ( x e. A |-> B ) $.
   $( Domain of a class given by the "maps to" notation. $)
   cmpdom $p |- ( A. x e. A B e. V <-> dom F = A ) $=
     ( cvv wcel wral cdm wceq wfun wfn wa df-fn fopab2ga ancom 3bitr4 cmpfun
     mpbiran2 ) CFGABHZDIBJZDKZDBLUBUAMTUAUBMDBNABCDEOUAUBPQABCDERS $.
     $( [17-Feb-2008] $) $( [17-Feb-2008] $)
 $}

 ${ $d A y $. $d B y $. $d x y $.
   cmpran.1 $e |- F = ( x e. A |-> B ) $.
   $( Range of a class given by the "maps to" notation. $)
   cmpran $p |- ran F = { y | E. x e. A y = B } $=
     ( crn cmpt cv wcel wceq wa copab wrex cab rneqi df-mpt rnopab2 3eqtr ) EGA
     CDHZGAICJBIDKZLABMZGUAACNBOETFPTUBABCDQPABCDRS $.
     $( [17-Feb-2008] $) $( [17-Feb-2008] $)
 $}

 ${
   minr.1 $e |- B e. V $.
   $( Value and operands of minus belong to ` CC ` . $)
   minbr $p |- ( ( A - B ) e. CC <-> ( A e. CC /\ B e. CC ) ) $=
     ( cmin co cc wcel wa cxp wf cdm wceq subopr fdm ax-mp 0ncn ndmoprrcl
     subclt impbi ) ABDEFGAFGBFGHABFDCFFIZFDJDKTLMTFDNOPQABRS $.
     $( [17-Feb-2008] $) $( [17-Feb-2008] $)
 $}

 $( Value and operands of minus belong to ` CC ` . $)
 minbr1 $p |- ( B e. V -> ( ( A - B ) e. CC <->
   ( A e. CC /\ B e. CC ) ) ) $=
   ( cvv wcel cmin co cc wa wb c1 cif wceq opreq2 eleq1d eleq1 anbi2d bibi12d
   ax1cn elisseti elimel minbr dedth ) BCDZABEFZGDZAGDZBGDZHZIAUCBJKZEFZGDZUFUI
   GDZHZIBJBUILZUEUKUHUMUNUDUJGBUIAEMNUNUGULUFBUIGOPQAUIBJCJGRSTUAUB $.
   $( [17-Feb-2008] $) $( [17-Feb-2008] $)

  ${
    $( A function's value belongs to its range. A more general version
    of ~ fnoprvalrn . To be used with partial operations. $)
    fnoprvalrn2 $p |- ( ( Fun F /\ <. A , B >. e. dom F ) ->
      ( A F B ) e. ran F ) $=
      ( wfun cop cdm wcel wa cfv crn co fvelrn df-opr syl5eqel ) CDABEZCFGHOCIC
      JABCKOCLABCMN $.
      $( [10-Mar-2008] $) $( [10-Mar-2008] $)
  $}


