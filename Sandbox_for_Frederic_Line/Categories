$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Categories
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Cat $. $( The class of all categories. $)

  $( Extend class notation with the class of categories. $)
  ccat $a class Cat $.

  ${
    $d x d c j r f g h a $.
    $( A category is a deductive system where the composition is associative,
    and the identity a neutral element. $)
    df-cat $a |- Cat = { x | E. d E. c E. j E. r
    ( x = <. <. d , c >. , <. j , r >. >. /\
           ( ( <. <. d , c >. , <. j , r >. >. e. Ded /\
           A. f e. dom d A. g e. dom d A. h e. dom d
              (
                ( ( d ` h ) = ( c ` g ) /\ ( d ` g ) = ( c ` f ) )
              ->
                ( h r ( g r f ) ) = ( ( h r g ) r f )
               ) ) /\

          ( A. a e. dom j A. f e. dom d ( ( c ` f ) = a ->
             ( ( j ` a ) r f ) = f ) /\

          A. a e. dom j A. f e. dom d ( ( d ` f ) = a ->
             ( f r ( j ` a ) ) = f ) ) ) ) } $.
  $}

  ${ $d C a c d f g h j r $. $d D a c d f g h j r $. $d J a c d f j r $.
     $d M c d j r f $. $d O c d j r $. $d R a c d f g h j r $.
     $d a c d f g h j r x $.
    iscat.1 $e |- M = dom D $.
    iscat.2 $e |- O = dom J $.
    $( The predicate "is a category". $)
    iscat $p |-  ( ( ( D e. A /\ C e. B /\ J e. F ) /\ R e. G )
      -> ( <. <. D , C >. , <. J , R >. >. e. Cat
        <-> ( ( <. <. D , C >. , <. J , R >. >. e. Ded /\
           A. f e. M A. g e. M A. h e. M
              (
                ( ( D ` h ) = ( C ` g ) /\ ( D ` g ) = ( C ` f ) )
              ->
                ( h R ( g R f ) ) = ( ( h R g ) R f )
               ) ) /\

          ( A. a e. O A. f e. M ( ( C ` f ) = a ->
             ( ( J ` a ) R f ) = f ) /\

          A. a e. O A. f e. M ( ( D ` f ) = a ->
             ( f R ( J ` a ) ) = f ) ) ) ) ) $=
      ( vx vd vc vj vr wcel w3a wa cop cv wceq cded cfv co wi cdm wral wex cab
      ccat opeq1 opeq1d eleq1d dmeq syl6eqr eleq2d fveq1 eqeq1d anbi12d imbi1d
      imbi12d ralbidv2 raleq1d ralbidv opeq2 eqeq2d 2ralbidv anbi1d opeq2d
      opreq1d imbi2d opreq2d opreq eqtrd eqeq12d elo df-cat eleq2i syl5bb ) DAU
      BCBUBKIUBUCEJUBUDDCUEZKEUEZUEZQUFRUFZSUFZUEZTUFZUAUFZUEZUEZUGWOUHUBZHUFZW
      IUIZGUFZWJUIZUGZWSWIUIZFUFZWJUIZUGZUDZWQWSXCWMUJZWMUJZWQWSWMUJZXCWMUJZUGZ
      UKZHWIULZUMZGXMUMZFXMUMZUDZXDNUFZUGZXRWLUIZXCWMUJZXCUGZUKZFXMUMZNWLULZUMZ
      XCWIUIZXRUGZXCXTWMUJZXCUGZUKZFXMUMZNYEUMZUDZUDZUDUAUNTUNSUNRUNQUOZUBWHUHU
      BZWQDUIZWSCUIZUGZWSDUIZXCCUIZUGZUDZWQWSXCEUJZEUJZWQWSEUJZXCEUJZUGZUKZHLUM
      ZGLUMFLUMZUDZUUBXRUGZXRKUIZXCEUJZXCUGZUKZFLUMNMUMZXCDUIZXRUGZXCUUOEUJZXCU
      GZUKZFLUMNMUMZUDZUDZWHUPUBYODWJUEZWNUEZUHUBZYRWTUGZUUAXDUGZUDZXKUKZHLUMZG
      LUMZFLUMZUDZYCFLUMZNYEUMZUVAYJUKZFLUMZNYEUMZUDZUDWFWNUEZUHUBZUUDXKUKZHLUM
      ZGLUMFLUMZUDZUUNYBUKZFLUMZNYEUMZUWCUDZUDWFKWMUEZUEZUHUBZUWIUDZUUNUUOXCWMU
      JZXCUGZUKZFLUMZNMUMZUVAXCUUOWMUJZXCUGZUKZFLUMZNMUMZUDZUDUVGQRSUATDCKEABIJ
      WIDUGZXQUVRYNUWDUXJWPUVJXPUVQUXJWOUVIUHUXJWKUVHWNWIDWJUQURUSUXJXOUVPFXMLU
      XJXCXMUBZXCLUBZXOUVPUXJXMLXCUXJXMDULLWIDUTOVAZVBZUXJXNUVOGXMLUXJWSXMUBWSL
      UBXNUVOUXJXMLWSUXMVBUXJXLUVNHXMLUXJWQXMUBWQLUBXLUVNUXJXMLWQUXMVBUXJXFUVMX
      KUXJXAUVKXEUVLUXJWRYRWTWQWIDVCVDUXJXBUUAXDWSWIDVCVDVEVFVGVHVGVHVGVHVEUXJY
      FUVTYMUWCUXJYDUVSNYEUXJYCFXMLUXMVIVJUXJYLUWBNYEUXJYKUWAFXMLUXJUXKUXLYKUWA
      UXNUXJYHUVAYJUXJYGUUTXRXCWIDVCVDVFVGVHVJVEVEWJCUGZUVRUWJUWDUWNUXOUVJUWFUV
      QUWIUXOUVIUWEUHUXOUVHWFWNWJCDVKURUSUXOUVOUWHFGLLUXOUVNUWGHLUXOUVMUUDXKUXO
      UVKYTUVLUUCUXOWTYSYRWSWJCVCVLUXOXDUUBUUAXCWJCVCZVLVEVFVJVMVEUXOUVTUWMUWCU
      XOYCUWKNFYELUXOXSUUNYBUXOXDUUBXRUXPVDVFVMVNVEWLKUGZUWJUWRUWNUXIUXQUWFUWQU
      WIUXQUWEUWPUHUXQWNUWOWFWLKWMUQVOUSVNUXQUWMUXCUWCUXHUXQUWLUXBNYEMUXQXRYEUB
      ZXRMUBZUWLUXBUXQYEMXRUXQYEKULMWLKUTPVAVBZUXQUWKUXAFLUXQYBUWTUUNUXQYAUWSXC
      UXQXTUUOXCWMXRWLKVCZVPVDVQVJVGVHUXQUWBUXGNYEMUXQUXRUXSUWBUXGUXTUXQUWAUXFF
      LUXQYJUXEUVAUXQYIUXDXCUXQXTUUOXCWMUYAVRVDVQVJVGVHVEVEWMEUGZUWRUUMUXIUVFUY
      BUWQYQUWIUULUYBUWPWHUHUYBUWOWGWFWMEKVKVOUSUYBUWHUUKFGLLUYBUWGUUJHLUYBXKUU
      IUUDUYBXHUUFXJUUHUYBXHWQUUEWMUJUUFUYBXGUUEWQWMWSXCWMEVSVRWQUUEWMEVSVTUYBX
      JXIXCEUJUUHXIXCWMEVSUYBXIUUGXCEWQWSWMEVSVPVTWAVQVJVMVEUYBUXCUUSUXHUVEUYBU
      XAUURNFMLUYBUWTUUQUUNUYBUWSUUPXCUUOXCWMEVSVDVQVMUYBUXFUVDNFMLUYBUXEUVCUVA
      UYBUXDUVBXCXCUUOWMEVSVDVQVMVEVEWBUPYPWHQFGHTUANSRWCWDWE $.
      $( [28-Oct-2007] $) $( [24-Oct-2007] $)
  $}

  ${ $d C a c d f g h j r $. $d D a c d f g h j r $. $d J a c d f j r $.
     $d M c d j r f $. $d O c d j r $. $d R a c d f g h j r $.
     $d T c d j r x $. $d a c d f g h j r x $.
     cati.1 $e |- D = ( dom_ ` T ) $.
     cati.2 $e |- C = ( cod_ ` T ) $.
     cati.3 $e |- J = ( id_ ` T ) $.
     cati.4 $e |- R = ( o_ ` T ) $.
     cati.5 $e |- M = dom D $.
     cati.6 $e |- O = dom J $.
     $( Properties of a category. $)
     cati $p |- ( T e. Cat
       -> ( ( <. <. D , C >. , <. J , R >. >. e. Ded /\
           A. f e. M A. g e. M A. h e. M
              (
                ( ( D ` h ) = ( C ` g ) /\ ( D ` g ) = ( C ` f ) )
              ->
                ( h R ( g R f ) ) = ( ( h R g ) R f )
               ) ) /\

          ( A. a e. O A. f e. M ( ( C ` f ) = a ->
             ( ( J ` a ) R f ) = f ) /\

          A. a e. O A. f e. M ( ( D ` f ) = a ->
             ( f R ( J ` a ) ) = f ) ) ) ) $=
       ( vx vd vc vj vr ccat wcel cv cop wceq cded cfv wa co wi cdm wral wex
       cab df-cat eleq2i c1st wb domval eqcomi eqeq2i opeq1 opeq1d eleq1d dmeq
       syl6eqr eleq2d fveq1 eqeq1d anbi12d imbi1d imbi12d ralbidv2 raleq1d
       ralbidv sylbi c2nd codval opeq2 eqeq2d 2ralbidv anbi1d idval opeq2d
       opreq1d imbi2d opreq2d cmpval opreq eqtrd eqeq12d eloi ) DUCUDDRUESUEZTU
       EZUFZUAUEZUBUEZUFZUFZUGXAUHUDZGUEZWOUIZFUEZWPUIZUGZXEWOUIZEUEZWPUIZUGZUJ
       ZXCXEXIWSUKZWSUKZXCXEWSUKZXIWSUKZUGZULZGWOUMZUNZFXSUNZEXSUNZUJZXJKUEZUGZ
       YDWRUIZXIWSUKZXIUGZULZEXSUNZKWRUMZUNZXIWOUIZYDUGZXIYFWSUKZXIUGZULZEXSUNZ
       KYKUNZUJZUJZUJUBUOUAUOTUOSUORUPZUDBAUFZHCUFZUFZUHUDZXCBUIZXEAUIZUGZXEBUI
       ZXIAUIZUGZUJZXCXEXICUKZCUKZXCXECUKZXICUKZUGZULZGIUNZFIUNEIUNZUJZUUKYDUGZ
       YDHUIZXICUKZXIUGZULZEIUNKJUNZXIBUIZYDUGZXIUVDCUKZXIUGZULZEIUNKJUNZUJZUJZ
       UCUUBDREFGUAUBKTSUQURUUABWPUFZWTUFZUHUDZUUGXFUGZUUJXJUGZUJZXQULZGIUNZFIU
       NZEIUNZUJZYIEIUNZKYKUNZUVJYPULZEIUNZKYKUNZUJZUJZUUCWTUFZUHUDZUUMXQULZGIU
       NZFIUNEIUNZUJZUVCYHULZEIUNZKYKUNZUWLUJZUJZUUCHWSUFZUFZUHUDZUWSUJZUVCUVDX
       IWSUKZXIUGZULZEIUNZKJUNZUVJXIUVDWSUKZXIUGZULZEIUNZKJUNZUJZUJZUVPRSTUBUAD
       WODUSUIZUSUIZUGWOBUGZUUAUWNUTUYCBWOBUYCBDLVAVBVCUYDYCUWGYTUWMUYDXBUVSYBU
       WFUYDXAUVRUHUYDWQUVQWTWOBWPVDVEVFUYDYAUWEEXSIUYDXIXSUDZXIIUDZYAUWEUYDXSI
       XIUYDXSBUMIWOBVGPVHZVIZUYDXTUWDFXSIUYDXEXSUDXEIUDXTUWDUYDXSIXEUYGVIUYDXR
       UWCGXSIUYDXCXSUDXCIUDXRUWCUYDXSIXCUYGVIUYDXLUWBXQUYDXGUVTXKUWAUYDXDUUGXF
       XCWOBVJVKUYDXHUUJXJXEWOBVJVKVLVMVNVOVNVOVNVOVLUYDYLUWIYSUWLUYDYJUWHKYKUY
       DYIEXSIUYGVPVQUYDYRUWKKYKUYDYQUWJEXSIUYDUYEUYFYQUWJUYHUYDYNUVJYPUYDYMUVI
       YDXIWOBVJVKVMVNVOVQVLVLVRWPUYBVSUIZUGWPAUGZUWNUXEUTUYIAWPAUYIADMVTVBVCUY
       JUWGUWTUWMUXDUYJUVSUWPUWFUWSUYJUVRUWOUHUYJUVQUUCWTWPABWAVEVFUYJUWDUWREFI
       IUYJUWCUWQGIUYJUWBUUMXQUYJUVTUUIUWAUULUYJXFUUHUUGXEWPAVJWBUYJXJUUKUUJXIW
       PAVJZWBVLVMVQWCVLUYJUWIUXCUWLUYJYIUXAKEYKIUYJYEUVCYHUYJXJUUKYDUYKVKVMWCW
       DVLVRWRDVSUIZUSUIZUGWRHUGZUXEUYAUTUYMHWRHUYMDHNWEVBVCUYNUWTUXIUXDUXTUYNU
       WPUXHUWSUYNUWOUXGUHUYNWTUXFUUCWRHWSVDWFVFWDUYNUXCUXNUWLUXSUYNUXBUXMKYKJU
       YNYDYKUDZYDJUDZUXBUXMUYNYKJYDUYNYKHUMJWRHVGQVHVIZUYNUXAUXLEIUYNYHUXKUVCU
       YNYGUXJXIUYNYFUVDXIWSYDWRHVJZWGVKWHVQVNVOUYNUWKUXRKYKJUYNUYOUYPUWKUXRUYQ
       UYNUWJUXQEIUYNYPUXPUVJUYNYOUXOXIUYNYFUVDXIWSUYRWIVKWHVQVNVOVLVLVRWSUYLVS
       UIZUGWSCUGZUYAUVPUTUYSCWSCUYSDCOWJVBVCUYTUXIUVBUXTUVOUYTUXHUUFUWSUVAUYTU
       XGUUEUHUYTUXFUUDUUCWSCHWAWFVFUYTUWRUUTEFIIUYTUWQUUSGIUYTXQUURUUMUYTXNUUO
       XPUUQUYTXNXCUUNWSUKUUOUYTXMUUNXCWSXEXIWSCWKWIXCUUNWSCWKWLUYTXPUUPXIWSUKU
       UQUYTXOUUPXIWSXCXEWSCWKWGUUPXIWSCWKWLWMWHVQWCVLUYTUXNUVHUXSUVNUYTUXLUVGK
       EJIUYTUXKUVFUVCUYTUXJUVEXIUVDXIWSCWKVKWHWCUYTUXQUVMKEJIUYTUXPUVLUVJUYTUX
       OUVKXIXIUVDWSCWKVKWHWCVLVLVRWNVR $.
       $( [28-Oct-2007] $) $( [24-Oct-2007] $)
  $}

  $( An "algebra" with no object and no morphism. $)
  0alg $p |- <. <. (/) , (/) >. , <. (/) , (/) >. >. e. Alg $=
    ( c0 cop calg wcel wf w3a wfun cdm cxp wss crn cvv wa wb 0ex 3pm3.2i dm0
    eqcomi isalg mp2an f0 fun0 ssid xp0r 3sstr4 rn0 eqimssi mpbir2an ) AABZUIBC
    DZAAAEZUKUKFZAGZAHZAAIZJZAKZAJZFZALDZUTUTFUTUJULUSMNUTUTUTOOOPOLLAAALLAAAUN
    AQRZVASTUKUKUKAUAZVBVBPUMUPURUBAAUNUOAUCQAUDUEUQAUFUGPUH $.
    $( [10-Jan-2008] $) $( [10-Jan-2008] $)


  ${ $d a f g $.
  $( A deductive system with no object and no morphism. $)
  0ded $p |- <. <. (/) , (/) >. , <. (/) , (/) >. >. e. Ded $=
    ( va vg vf c0 cop cded wcel calg cv cfv wceq wa cdm wral wb w3a co wi cvv
    0ex 3pm3.2i eqid isded mp2an 0alg dm0 eleq2i noel pm2.21i sylbi rgen
    pm3.2i mpbir2an ) DDEZUNEZFGZUOHGZAIZDJDJURKZUSLZADMZNZBIZCIZEVAGVCDJZVDDJZ
    KZOBVANZCVANZPZVGVCVDDQDJZVFKRBVANZCVANZVGVKVEKRBVANZCVANZLZDSGZVQVQPVQUPVJ
    VPLOVQVQVQTTTUATSSDDDCBSSDVAVAAVAUBZVRUCUDUQVBVIUEUTAVAURVAGURDGZUTVADURUFU
    GVSUTURUHUIUJUKVHCVAVDVAGZVDDGZVHVADVDUFUGZWAVHVDUHZUIUJUKUAVMVOVLCVAVTWAVL
    WBWAVLWCUIUJUKVNCVAVTWAVNWBWAVNWCUIUJUKULUM $.
    $( [10-Jan-2008] $) $( [10-Jan-2008] $)
  $}

  ${ $d a f g h $.
  $( Category 0. No object, no morphism. $)
  0cat $p |- <. <. (/) , (/) >. , <. (/) , (/) >. >. e. Cat $=
    ( vh vg vf va c0 cop ccat wcel cded cv cfv wceq wa co wi cdm wral cvv w3a
    wb 0ex 3pm3.2i eqid iscat mp2an 0ded dm0 eleq2i noel pm2.21i sylbi rgen
    pm3.2i mpbir2an ) EEFZUOFZGHZUPIHZAJZEKBJZEKZLVACJZEKZLMUSUTVBENENUSUTENVBE
    NLOAEPZQBVDQZCVDQZMZVCDJZLZVHEKZVBENVBLOCVDQZDVDQZVIVBVJENVBLOCVDQZDVDQZMZE
    RHZVPVPSVPUQVGVOMTVPVPVPUAUAUAUBUARREEECBARREVDVDDVDUCZVQUDUEURVFUFVECVDVBV
    DHVBEHZVEVDEVBUGUHVRVEVBUIUJUKULUMVLVNVKDVDVHVDHZVHEHZVKVDEVHUGUHZVTVKVHUIZ
    UJUKULVMDVDVSVTVMWAVTVMWBUJUKULUMUN $.
    $( [10-Jan-2008] $) $( [10-Jan-2008] $)
  $}


  ${ $d A u x y z $.
    1cat.1 $e |- A e. V $.
    $( Category 1. A category with one object and one morphism. $)
    1cat $p |- <. <. { <. <. A , A >. , A >. } ,
     { <. <. A , A >. , A >. } >. ,  <. { <. A , <. A , A >. >. } ,
     { <. <. <. A , A >. , <. A , A >. >. , <. A , A >. >. } >. >.
     e. Cat $=
      ( vu vz vy vx cop csn ccat wcel cded cv cfv wceq wa co wi wral cvv w3a
      wb snex 3pm3.2i cdm dmsnop eqcomi iscat mp2an 1ded id eqcomd 3ad2ant3
      3ad2ant2 3ad2ant1 opreq12d opreq1d opreq2d 3eqtrd eqeq12d df-opr opex
      fvsn eqtr2 eqtr opreq12i a1i syl5bi elsni syl3an rgen3 pm3.2i adantr
      fveq2d adantl opreq1i 3eqtr syl2an rgen2 opreq2i mpbir2an ) AAGZAGZHZWCGA
      WAGZHZWAWAGZWAGZHZGGZIJZWIKJZCLZWCMDLZWCMZNWNELZWCMZNOZWLWMWOWHPZWHPZWLWM
      WHPZWOWHPZNZQZCWAHZRDXDREXDRZOZWPFLZNZXGWEMZWOWHPZWONZQZEXDRFAHZRZXHWOXIW
      HPZWONZQZEXDRFXMRZOZWCSJZXTWESJZTWHSJWJXFXSOUAXTXTYAWBUBZYBWDUBUCWGUBSSWC
      WCWHEDCSSWEXDXMFWCUDXDWAAUEUFWEUDXMAWAUEUFUGUHWKXEABUIXCEDCXDXDXDWOWANZWM
      WANZWLWANZXCWOXDJZWMXDJWLXDJYCYDYETZWAWAWAWHPZWHPZYHWAWHPZNZXBWQYGYIWSYJX
      AYGWAWLYHWRWHYEYCWAWLNYDYEWLWAYEUJUKZULYGWAWMWAWOWHYDYCWAWMNYEYDWMWAYDUJU
      KZUMYCYDWAWONZYEYCWOWAYCUJUKZUNUOUOYGYJWLWAWHPZWAWHPZWTWAWHPZXAYEYCYJYQNY
      DYEYHYPWAWHYEWAWLWAWHYLUPUPULYDYCYQYRNYEYDYPWTWAWHYDWAWMWLWHYMUQUPUMYCYDY
      RXANYEYCWAWOWTWHYOUQUNURUSYKWQWAYHYHWAWHYHWFWHMZWAWAWAWHUTZWFWAWAWAVAAAVA
      ZVBZVCYHYSWAYTUUBVDVEVFVGWOWAVHZWMWAVHWLWAVHVIVJVKXNXRXLFEXMXDXGANZYCXLXG
      XMJZYFUUDYCOZAWEMZWAWHPZWANZXKXHUUFUUHXJWAWOUUFUUGXIWAWOWHUUFAXGWEUUDAXGN
      YCUUDXGAUUDUJUKVLVMZYCYNUUDYOVNZUOUUKUSUUIXHUUHYHYSWAUUGWAWAWHAWABUUAVBZV
      OYTUUBVPVFVGXGAVHZUUCVQVRXQFEXMXDUUDYCXQUUEYFUUFWAUUGWHPZWANZXPXHUUFUUNXO
      WAWOUUFWAWOUUGXIWHUUKUUJUOUUKUSUUOXHUUNYHYSWAUUGWAWAWHUULVSYTUUBVPVFVGUUM
      UUCVQVRVKVT $.
      $( [31-Oct-2007] $) $( [30-Oct-2007] $)
  $}

  ${
  $d f g t u v w x y z $.
  $( Structure of a category. $)
  strcat $p |- Cat (_ ( ( V X. V ) X. ( V X. V ) ) $=
    ( vx vg vf vv vu vw vz vy vt ccat cv cop wceq cded wcel cfv wa co wi cdm
    wral wex cab cvv cxp df-cat stcat eqsstr ) JAKBKZCKZLDKZEKZLLZMUMNOFKZUIPGK
    ZUJPMUOUIPHKZUJPZMQUNUOUPULRULRUNUOULRUPULRMSFUITZUAGURUAHURUAQUQIKZMUSUKPZ
    UPULRUPMSHURUAIUKTZUAUPUIPUSMUPUTULRUPMSHURUAIVAUAQQZQEUBDUBCUBBUBAUCUDUDUE
    ZVCUEAHGFDEICBUFVBABCEDUGUH $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)
  $}

  $( A category is a relation. $)
  relcat $p |- Rel Cat $=
    ( ccat wrel cvv cxp wss strcat xpss sstri df-rel mpbir ) ABACCDZEAKKDKFKKGH
    AIJ $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)

  $( The domain of a category is a relation. $)
  reldcat $p |- Rel dom Cat $=
    ( ccat cdm wrel cvv cxp wss strcat dmss ax-mp dmxpid sseqtr df-rel mpbir )
    ABZCNDDEZFNOOEZBZOAPFNQFGAPHIOJKNLM $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)

  $( The range of a category is a relation. $)
  relrcat $p |- Rel ran Cat $=
    ( ccat crn wrel cvv cxp wss strcat rnss ax-mp c0 wne wceq wa wcel 0ex ne0i
    pm3.2i xpnz mpbi rnxp sseqtr df-rel mpbir ) ABZCUDDDEZFUDUEUEEZBZUEAUFFUDUG
    FGAUFHIUEJKZUGUELDJKZUIMUHUIUIJDNUIODJPIZUJQDDRSUEUETIUAUDUBUC $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)

  ${
  $d T w x y z $.
  $( A category is a deductive system. $)
  catded $p |- ( T e. Cat -> T e. Ded ) $=
    ( vz vy vx vw ccat wcel cdom_ cfv ccod_ cop cid_ co_ cded c1st c2nd wrel
    cdm crn w3a wceq relcat reldcat relrcat 3pm3.2i 11st22nd mpan eqid domval
    codval opeq12i idval cmpval syl6eqr cv wa co wi wral cati pm3.26d eqeltrd
    ) AFGZAAHIZAJIZKZALIZAMIZKZKZNVCAAOIZOIZVKPIZKZAPIZOIZVOPIZKZKZVJFQZFRQZFSQ
    ZTVCAVSUAVTWAWBUBUCUDUEAFUFUGVFVNVIVRVDVLVEVMVDAVDUHZUIVEAVEUHZUJUKVGVPVHVQ
    AVGVGUHZULAVHVHUHZUMUKUKUNVCVJNGZBUOZVDICUOZVEIUAWIVDIDUOZVEIZUAUPWHWIWJVHU
    QVHUQWHWIVHUQWJVHUQUAURBVDRZUSCWLUSDWLUSZVCWGWMUPWKEUOZUAWNVGIZWJVHUQWJUAUR
    DWLUSEVGRZUSWJVDIWNUAWJWOVHUQWJUAURDWLUSEWPUSUPVEVDVHADCBVGWLWPEWCWDWEWFWLU
    HWPUHUTVAVAVB $.
    $( [28-Oct-2007] $) $( [26-Oct-2007] $)
  $}

  ${
    cat.1 $e |- M = dom D $.
    cat.3 $e |- D = ( dom_ ` T ) $.
    ${
      cat.2 $e |- O = dom J $.
      cat.5 $e |- J = ( id_ ` T ) $.
      ${
        $( The 1st "axiom" of a category - ` ( dom_ `` T ) ` is a mapping
           from the morphisms of ` T ` to the objects of ` T ` . $)
        domc $p |- ( T e. Cat -> D : M --> O ) $=
          ( ccat wcel cded calg wf catded dedalg doma 3syl ) BJKBLKBMKDEANBOBPA
          BCDEFGHIQR $.
          $( [3-Jan-2008] $) $( [2-Jan-2008] $)
      $}

      ${
        cat.4 $e |- C = ( cod_ ` T ) $.
        $( The 2nd "axiom" of a category - ` ( cod_ `` T ) ` is a mapping
        from the morphisms of ` T ` to  the objects of ` T ` . $)
        codc $p |- ( T e. Cat -> C : M --> O ) $=
          ( ccat wcel cded calg wf catded dedalg coda 3syl ) CLMCNMCOMEFAPCQCRA
          BCDEFGHIJKST $.
          $( [3-Jan-2008] $) $( [2-Jan-2008] $)
      $}

      ${
        $( The 3rd "axiom" of a category - ` id_ ` is a mapping from the
        objects of ` T ` to  the morphisms of ` T ` . $)
        idc $p |- ( T e. Cat -> J : O --> M ) $=
          ( ccat wcel cded calg wf catded dedalg ida 3syl ) BJKBLKBMKEDCNBOBPAB
          CDEFGHIQR $.
          $( [5-Dec-2007] $) $( [5-Dec-2007] $)
      $}
   $}
   ${
        cat.6 $e |- R = ( o_ ` T ) $.
        $( The 4th "axiom" of a category - ` ( o_ `` T )  ` is a partial
        operation from the morphisms of ` T ` to the morphisms of ` T ` .
        $)
        cmppfc $p |- ( T e. Cat ->
        ( Fun R /\ dom R (_ ( M X. M ) /\ ran R (_ M ) ) $=
          ( ccat wcel cded calg wfun cdm cxp wss crn w3a catded dedalg cmppfa
          3syl ) CHICJICKIBLBMDDNOBPDOQCRCSABCDEFGTUA $.
          $( [10-Mar-2008] $) $( [10-Mar-2008] $)
       $}
   $}

  ${
    idosc.1 $e |- O = dom J $.
    idosc.2 $e |- D = ( dom_ ` T ) $.
    idosc.3 $e |- J = ( id_ ` T ) $.
    idosc.4 $e |- C = ( cod_ ` T ) $.
    $( The 5th "axiom" of a category - The identity is a morphism whose
    domain and codomain are equal. $)
    idosc $p |- ( ( T e. Cat /\ A e. O ) ->  ( ( D ` ( J ` A ) ) = A /\
        ( C ` ( J ` A ) ) = A ) ) $=
      ( cded wcel cfv wceq wa ccat idosd catded sylan ) DKLAFLAEMZCMANTBMANODPL
      ABCDEFGHIJQDRS $.
      $( [5-Dec-2007] $) $( [5-Dec-2007] $)
  $}

  ${
    cmppfcd.1 $e |- M = dom D $.
    cmppfcd.2 $e |- D = ( dom_ ` T ) $.
    cmppfcd.3 $e |- C = ( cod_ ` T ) $.
    cmppfcd.4 $e |- R = ( o_ ` T ) $.
    $( The 6th "axiom" of a category - ` ( G ( o_ `` T ) F )  ` is only
    defined when the domain of ` F ` is the codomain of ` G ` . $)
    cmppfcd $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
           ( <. G , F >. e. dom R <-> ( D ` G ) = ( C ` F ) ) ) $=
      ( cded wcel cop cdm cfv wceq wb ccat cmppfd catded syl3an1 ) DLMEGMFGMFEN
      COMFBPEAPQRDSMABCDEFGHIJKTDUAUB $.
      $( [10-Mar-2008] $) $( [10-Mar-2008] $)


    $( The 7th "axiom" of a category - When ` ( G ( o_ `` T ) F ) ` is defined
    its domain is the domain of ` F ` . $)
    domcmpc $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
        ( ( D ` G ) = ( C ` F ) -> ( D ` ( G R F ) ) = ( D ` F ) ) ) $=
      ( cded wcel cfv wceq co wi ccat domcmpd catded syl3an1 ) DLMEGMFGMFBNEANO
      FECPBNEBNOQDRMABCDEFGHIJKSDTUA $.
      $( [10-Mar-2008] $) $( [10-Mar-2008] $)


    $( The 8th "axiom" of a category - When ` ( G ( o_ `` T ) F ) ` is defined
    its codomain is the codomain of ` G ` . $)
    codcmpc $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
              ( ( D ` G ) = ( C ` F )
              -> ( C ` ( G R F ) ) = ( C ` G ) ) ) $=
      ( cded wcel cfv wceq co wi ccat codcmpd catded syl3an1 ) DLMEGMFGMFBNEANO
      FECPANFANOQDRMABCDEFGHIJKSDTUA $.
      $( [10-Jan-2008] $) $( [10-Jan-2008] $)
  $}


  ${ $d C f g h x $. $d D f g h x $. $d F f g h $.
     $d G g h $. $d H h $. $d M f g h $. $d R f g h x $.
     $d T f x $.
    cmpasso.1 $e |- M = dom D $.
    cmpasso.2 $e |- D = ( dom_ ` T ) $.
    cmpasso.5 $e |- C = ( cod_ ` T ) $.
    cmpasso.6 $e |- R = ( o_ ` T ) $.
    $( The 9th "axiom" of a category - ` o_ ` is associative. $)
    cmpasso $p |-  ( ( T e. Cat /\ ( F e. M /\ G e. M /\ H e. M ) ) ->
              (
                ( ( D ` H ) = ( C ` G ) /\
                ( D ` G ) = ( C ` F ) )
              ->
                ( H R ( G R F ) ) =
                  ( ( H R G ) R F )
               ) ) $=
      ( vh vg vf vx ccat wcel w3a cfv wceq wa co wi cop cid_ cded cv wral cdm
      eqid cati fveq2 eqeq2d anbi2d opreq2 opreq2d eqeq12d imbi12d eqeq1d
      anbi12d opreq1 opreq1d anbi1d rcla43v com12 ad2antlr syl imp ) DQRZEHRFHR
      GHRSZGBTZFATZUAZFBTZEATZUAZUBZGFECUCZCUCZGFCUCZECUCZUAZUDZVJBAUEDUFTZCUEU
      EUGRZMUHZBTZNUHZATZUAZWIBTZOUHZATZUAZUBZWGWIWMCUCZCUCZWGWICUCZWMCUCZUAZUD
      ZMHUINHUIOHUIZUBWNPUHZUAXDWETZWMCUCWMUAUDOHUIPWEUJZUIWMBTXDUAWMXECUCWMUAU
      DOHUIPXFUIUBZUBVKWDUDZABCDONMWEHXFPJKWEUKLIXFUKULXCXHWFXGVKXCWDXBWDWKWLVP
      UAZUBZWGWIECUCZCUCZWSECUCZUAZUDWHVMUAZVQUBZWGVSCUCZWGFCUCZECUCZUAZUDONMEF
      GHHHWMEUAZWPXJXAXNYAWOXIWKYAWNVPWLWMEAUMUNUOYAWRXLWTXMYAWQXKWGCWMEWICUPUQ
      WMEWSCUPURUSWIFUAZXJXPXNXTYBWKXOXIVQYBWJVMWHWIFAUMUNYBWLVOVPWIFBUMUTVAYBX
      LXQXMXSYBXKVSWGCWIFECVBUQYBWSXRECWIFWGCUPVCURUSWGGUAZXPVRXTWCYCXOVNVQYCWH
      VLVMWGGBUMUTVDYCXQVTXSWBWGGVSCVBYCXRWAECWGGFCVBVCURUSVEVFVGVHVI $.
      $( [30-Oct-2007] $) $( [29-Oct-2007] $)
  $}

  ${ $d A a f $. $d C a f x y $. $d D a f x y $. $d F f $.
     $d J a f $. $d M a f $. $d O a $. $d R a f x y $.
    cmpida.1 $e |- M = dom D $.
    cmpida.2 $e |- D = ( dom_ ` T ) $.
    cmpida.3 $e |- O = dom  J $.
    cmpida.4 $e |- J = ( id_ ` T ) $.
    cmpida.5 $e |- R = ( o_ ` T ) $.
    cmpida.6 $e |- C = ( cod_ ` T ) $.
    $( The 10th "axiom" of a category - Composition of a morphism with the
    identity. $)
    cmpida $p |- ( ( T e. Cat /\ A e. O /\ F e. M ) -> ( ( C ` F ) = A
         -> ( ( J ` A ) R F ) = F ) ) $=
      ( vy vx vf va ccat wcel cfv wceq co wi cop cded cv wa wral cati eqeq2
      fveq2 opreq1d eqeq1d imbi12d opreq2 id eqeq12d rcla42v com12 ad2antrl
      syl 3impib ) ETUAZAIUAZFHUAZFBUBZAUCZAGUBZFDUDZFUCZUEZVECBUFGDUFUFUGUAPUH
      ZCUBQUHZBUBUCVOCUBRUHZBUBZUCUIVNVOVPDUDDUDVNVODUDVPDUDUCUEPHUJQHUJRHUJUIZ
      VQSUHZUCZVSGUBZVPDUDZVPUCZUEZRHUJSIUJZVPCUBVSUCVPWADUDVPUCUERHUJSIUJZUIUI
      VFVGUIZVMUEZBCDERQPGHISKOMNJLUKWEWHVRWFWGWEVMWDVMVQAUCZVJVPDUDZVPUCZUESRA
      FIHVSAUCZVTWIWCWKVSAVQULWLWBWJVPWLWAVJVPDVSAGUMUNUOUPVPFUCZWIVIWKVLWMVQVH
      AVPFBUMUOWMWJVKVPFVPFVJDUQWMURUSUPUTVAVBVCVD $.
      $( [30-Oct-2007] $) $( [29-Oct-2007] $)
  $}

  ${ $d A a f $. $d D a f x y $. $d F f $. $d J a f $.
     $d M a f $. $d O a $. $d R a f x y $. $d T a f x y $.
    cmpidb.1 $e |- M = dom D $.
    cmpidb.2 $e |- D = ( dom_ ` T ) $.
    cmpidb.3 $e |- O = dom  J $.
    cmpidb.4 $e |- J = ( id_ ` T ) $.
    cmpidb.5 $e |- R = ( o_ ` T ) $.
    $( The 11th "axiom" of a category - Composition of a morphism with the
    identity. $)
      cmpidb $p |- ( ( T e. Cat /\ A e. O /\ F e. M ) -> ( ( D ` F ) = A
         -> ( F R ( J ` A ) ) = F ) ) $=
        ( vy vx vf va ccat wcel cfv wceq co wi ccod_ cop cded cv wa wral eqid
        cati eqeq2 fveq2 opreq2d eqeq1d imbi12d opreq1 id eqeq12d rcla42v
        com12 ad2antll syl 3impib ) DRSZAHSZEGSZEBTZAUAZEAFTZCUBZEUAZUCZVEBDUDT
        ZUEFCUEUEUFSNUGZBTOUGZVNTUAVPBTPUGZVNTZUAUHVOVPVQCUBCUBVOVPCUBVQCUBUAUC
        NGUIOGUIPGUIUHZVRQUGZUAVTFTZVQCUBVQUAUCPGUIQHUIZVQBTZVTUAZVQWACUBZVQUAZ
        UCZPGUIQHUIZUHUHVFVGUHZVMUCZVNBCDPONFGHQJVNUJLMIKUKWHWJVSWBWIWHVMWGVMWC
        AUAZVQVJCUBZVQUAZUCQPAEHGVTAUAZWDWKWFWMVTAWCULWNWEWLVQWNWAVJVQCVTAFUMUN
        UOUPVQEUAZWKVIWMVLWOWCVHAVQEBUMUOWOWLVKVQEVQEVJCUQWOURUSUPUTVAVBVCVD $.
        $( [28-Oct-2007] $) $( [24-Oct-2007] $)
  $}

  ${
    dmo.1 $e |- M = dom ( dom_ ` T ) $.
    dmo.3 $e |- O = dom ( id_ ` T ) $.

    ${
      dmo.2 $e |- D = ( dom_ ` T ) $.
      $( The domain of a morphism is an object. $)
      dmo $p |- ( ( T e. Cat /\ F e. M ) -> ( D ` F ) e. O ) $=
        ( wf wcel cfv ccat ffvelrn cid_ cdom_ cdm eqcomi dmeqi eqtr eqid domc
        sylan ) DEAICDJCAKEJBLJDECAMABBNKZDEDBOKZPAPFUDAAUDHQRSHGUCTUAUB $.
        $( [3-Jan-2008] $) $( [2-Jan-2008] $)

    $}

    ${
      cdmo.2 $e |- C = ( cod_ ` T ) $.
      $( The codomain of a morphism is an object. $)
      cdmo $p |- ( ( T e. Cat /\ F e. M ) -> ( C ` F ) e. O ) $=
        ( wf wcel cfv ccat ffvelrn cdom_ cid_ eqid codc sylan ) DEAICDJCAKEJBLJ
        DECAMABNKZBBOKZDEFSPGTPHQR $.
        $( [3-Jan-2008] $) $( [2-Jan-2008] $)
    $}

    ${
      jdmo.2 $e |- J = ( id_ ` T ) $.
      $( The identity is a morphism. $)
      jdmo $p |- ( ( T e. Cat /\ A e. O ) -> ( J ` A ) e. M ) $=
        ( wf wcel cfv ccat ffvelrn cdom_ eqid cid_ cdm dmeqi eqtr4 idc sylan )
        EDCIAEJACKDJBLJEDACMBNKZBCDEFUBOEBPKZQCQGCUCHRSHTUA $.
        $( [10-Jan-2008] $) $( [10-Jan-2008] $)
    $}

  $}

  ${
    cmpmorp.1 $e |- M = dom ( dom_ ` T ) $.
    cmpmorp.2 $e |- D = ( dom_ ` T ) $.
    cmpmorp.3 $e |- C = ( cod_ ` T ) $.
    cmpmorp.4 $e |- R = ( o_ ` T ) $.
    $( Condition for a composite to be a morphism. $)
    cmpmorp $p |- ( ( T e. Cat /\ F e. M /\ G e. M ) ->
      ( ( D ` G ) = ( C ` F ) -> ( G R F ) e. M ) ) $=
      ( ccat wcel w3a cfv wceq co crn wfun cop cdm wa cxp wss cdom_ eqid
      cmppfc 3ad2ant1 3simp1d adantr eqcomi dmeqi eqtr cmppfcd biimpar jca ex
      fnoprvalrn2 syl6 wi 3simp3 ssel 3syl syld ) DLMZEGMZFGMZNZFBOEAOPZFECQZCR
      ZMZVJGMZVHVICSZFETCUAZMZUBZVLVHVIVQVHVIUBVNVPVHVNVIVHVNVOGGUCUDZVKGUDZVEV
      FVNVRVSNZVGDUEOZCDGHWAUFKUGUHZUIUJVHVPVIABCDEFGGWAUABUAHWABBWAIUKULUMIJKU
      NUOUPUQFECURUSVHVTVSVLVMUTWBVNVRVSVAVKGVJVBVCVD $.
      $( [10-Mar-2008] $) $( [10-Mar-2008] $)
  $}


