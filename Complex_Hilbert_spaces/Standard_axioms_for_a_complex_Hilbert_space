$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Standard axioms for a complex Hilbert space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    hlex.1 $e |- X = ( Base ` U ) $.
    $( The base set of a Hilbert space is a set. $)
    hlex $p |- X e. V $=
      ( cba cfv cvv fvex eqeltr ) BADEFCADGH $.
      $( [17-Dec-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    hladdf.1 $e |- X = ( Base ` U ) $.
    hladdf.2 $e |- G = ( +v ` U ) $.
    $( Mapping for Hilbert space vector addition. $)
    hladdf $p |- ( U e. CHil -> G : ( X X. X ) --> X ) $=
      ( chl wcel cnv cxp wf hlnv nvgf syl ) AFGAHGCCICBJAKABCDELM $.
      $( [17-Sep-2007] $) $( [7-Sep-2007] $)

    $( Hilbert space vector addition is commutative. $)
    hlcom $p |- ( ( U e. CHil /\ A e. X /\ B e. X ) ->
                ( A G B ) = ( B G A ) ) $=
      ( cnv wcel co wceq chl nvcom hlnv syl3an1 ) CHIAEIBEIABDJBADJKCLIABCDEFGM
      CNO $.
      $( [8-Sep-2007] $) $( [7-Sep-2007] $)

    $( Hilbert space vector addition is associative. $)
    hlass $p |- ( ( U e. CHil /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                   ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( cnv wcel w3a co wceq chl nvass hlnv sylan ) DIJAFJBFJCFJKABELCELABCELEL
      MDNJABCDEFGHODPQ $.
      $( [11-Dec-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    hl0cl.1 $e |- X = ( Base ` U ) $.
    hl0cl.5 $e |- Z = ( 0v ` U ) $.
    $( The Hilbert space zero vector. $)
    hl0cl $p |- ( U e. CHil -> Z e. X ) $=
      ( chl wcel cnv hlnv nvzcl syl ) AFGAHGCBGAIABCDEJK $.
      $( [11-Dec-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    hladdid.1 $e |- X = ( Base ` U ) $.
    hladdid.2 $e |- G = ( +v ` U ) $.
    hladdid.5 $e |- Z = ( 0v ` U ) $.
    $( Hilbert space addition with the zero vector. $)
    hladdid $p |- ( ( U e. CHil /\ A e. X ) -> ( A G Z ) = A ) $=
      ( cnv wcel co wceq chl nv0rid hlnv sylan ) BIJADJAECKALBMJABCDEFGHNBOP $.
      $( [13-Dec-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    hlmulf.1 $e |- X = ( Base ` U ) $.
    hlmulf.4 $e |- S = ( .s ` U ) $.
    $( Mapping for Hilbert space scalar multiplication. $)
    hlmulf $p |- ( U e. CHil -> S : ( CC X. X ) --> X ) $=
      ( chl wcel cnv cc cxp wf hlnv nvsf syl ) BFGBHGICJCAKBLABCDEMN $.
      $( [12-Dec-2007] $) $( [7-Sep-2007] $)

    $( Hilbert space scalar multiplication by one. $)
    hlmulid $p |- ( ( U e. CHil /\ A e. X ) -> ( 1 S A ) = A ) $=
      ( cnv wcel c1 co wceq chl nvsid hlnv sylan ) CGHADHIABJAKCLHABCDEFMCNO $.
      $( [21-Dec-2007] $) $( [7-Sep-2007] $)

    $( Hilbert space scalar multiplication associative law. $)
    hlmulass $p |- ( ( U e. CHil /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
                   ( ( A x. B ) S C ) = ( A S ( B S C ) ) ) $=
      ( cnv wcel cc w3a cmul co wceq chl nvsass hlnv sylan ) EIJAKJBKJCFJLABMNC
      DNABCDNDNOEPJABCDEFGHQERS $.
      $( [21-Dec-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    hldi.1 $e |- X = ( Base ` U ) $.
    hldi.2 $e |- G = ( +v ` U ) $.
    hldi.4 $e |- S = ( .s ` U ) $.
    $( Hilbert space scalar multiplication distributive law. $)
    hldi $p |- ( ( U e. CHil /\ ( A e. CC /\ B e. X /\ C e. X ) ) ->
                  ( A S ( B G C ) ) = ( ( A S B ) G ( A S C ) ) ) $=
      ( cnv wcel cc w3a co wceq chl nvdi hlnv sylan ) EKLAMLBGLCGLNABCFODOABDOA
      CDOFOPEQLABCDEFGHIJREST $.
      $( [15-Dec-2007] $) $( [7-Sep-2007] $)

    $( Hilbert space scalar multiplication distributive law. $)
    hldir $p |- ( ( U e. CHil /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
                  ( ( A + B ) S C ) = ( ( A S C ) G ( B S C ) ) ) $=
      ( cnv wcel cc w3a caddc co wceq chl nvdir hlnv sylan ) EKLAMLBMLCGLNABOPC
      DPACDPBCDPFPQERLABCDEFGHIJSETUA $.
      $( [22-Dec-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    hlmul0.1 $e |- X = ( Base ` U ) $.
    hlmul0.4 $e |- S = ( .s ` U ) $.
    hlmul0.5 $e |- Z = ( 0v ` U ) $.
    $( Hilbert space scalar multiplication by zero. $)
    hlmul0 $p |- ( ( U e. CHil /\ A e. X ) -> ( 0 S A ) = Z ) $=
      ( cnv wcel cc0 co wceq chl nv0 hlnv sylan ) CIJADJKABLEMCNJABCDEFGHOCPQ
      $.
      $( [15-Dec-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    hlipf.1 $e |- X = ( Base ` U ) $.
    hlipf.7 $e |- P = ( .i ` U ) $.
    $( Mapping for Hilbert space inner product. $)
    hlipf $p |- ( U e. CHil -> P : ( X X. X ) --> CC ) $=
      ( chl wcel cnv cxp cc wf hlnv ipf syl ) BFGBHGCCIJAKBLABCDEMN $.
      $( [16-Dec-2007] $) $( [19-Nov-2007] $)

    $( Conjugate law for Hilbert space inner product. $)
    hlipcj $p |- ( ( U e. CHil /\ A e. X /\ B e. X ) ->
                  ( A P B ) = ( * ` ( B P A ) ) ) $=
      ( chl wcel w3a co ccj cfv wceq cnv ipcj hlnv syl3an1 3com23 eqcomd ) DHIZ
      AEIZBEIZJBACKLMZABCKZUAUCUBUDUENZDOIUCUBUFUABACDEFGPDQRST $.
      $( [21-Dec-2007] $) $( [8-Sep-2007] $)
  $}

  ${
    hlipdir.1 $e |- X = ( Base ` U ) $.
    hlipdir.2 $e |- G = ( +v ` U ) $.
    hlipdir.7 $e |- P = ( .i ` U ) $.
    $( Distributive law for Hilbert space inner product. $)
    hlipdir $p |- ( ( U e. CHil /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( A G B ) P C ) = ( ( A P C ) + ( B P C ) ) ) $=
      ( cphl wcel w3a co caddc wceq chl ipdir hlph sylan ) EKLAGLBGLCGLMABFNCDN
      ACDNBCDNONPEQLABCDEFGHIJREST $.
      $( [17-Dec-2007] $) $( [8-Sep-2007] $)
  $}

  ${
    hlipass.1 $e |- X = ( Base ` U ) $.
    hlipass.4 $e |- S = ( .s ` U ) $.
    hlipass.7 $e |- P = ( .i ` U ) $.
    $( Associative law for Hilbert space inner product. $)
    hlipass $p |- ( ( U e. CHil /\ ( A e. CC /\ B e. X /\ C e. X ) ) ->
                  ( ( A S B ) P C ) = ( A x. ( B P C ) ) ) $=
      ( cphl wcel cc w3a co cmul wceq chl ipass hlph sylan ) FKLAMLBGLCGLNABEOC
      DOABCDOPOQFRLABCDEFGHIJSFTUA $.
      $( [18-Dec-2007] $) $( [8-Sep-2007] $)
  $}

  ${
    hlipgt0.1 $e |- X = ( Base ` U ) $.
    hlipgt0.5 $e |- Z = ( 0v ` U ) $.
    hlipgt0.7 $e |- P = ( .i ` U ) $.
    $( The inner product of a Hilbert space vector by itself is positive. $)
    hlipgt0 $p |- ( ( U e. CHil /\ A e. X /\  A =/= Z ) -> 0 < ( A P A ) ) $=
      ( cnv wcel wne cc0 co clt wbr chl w3a cnm cfv c2 cexp cr sqgt0t eqid
      nvcl 3adant3 wa wceq nvz biimpd necon3d 3impia sylanc ipid breqtrrd hlnv
      syl3an1 ) CIJZADJZAEKZLAABMZNOCPJURUSUTQZLACRSZSZTUAMZVANVDUBJZVDLKZLVENO
      VBVDUCURUSVFUTACVCDFVCUDZUEUFURUSUTVGURUSUGZVDLAEVIVDLUHAEUHACVCDEFGVHUIU
      JUKULUMURUSVAVEUHUTABCVCDFVHHUNUFUOCUPUQ $.
      $( [18-Dec-2007] $) $( [8-Sep-2007] $)
  $}

  ${
    $d x D $.  $d x F $.  $d x X $.
    hlcompl.1 $e |- X = ( Base ` U ) $.
    hlcompl.8 $e |- D = ( IndMet ` U ) $.
    $( Completeness of a Hilbert space. $)
    hlcompl $p |- ( ( U e. CHil /\ F e. ( Cau ` D ) ) ->
                  E. x e. X F ( ~~>m ` D ) x ) $=
      ( chl wcel cca cfv wa cv clm wbr wrex cdm cms eqid cmscvg hlcms sylan wb
      cnv hlnv imsba rexeq1d syl adantr mpbird ) CHIZDBJKIZLDAMBNKOZAEPZUMABQQZ
      PZBRIULUPUKABDUOUOSTBCGUAUBUKUNUPUCZULUKCUDIZUQCUEURUMAEUOBCEFGUFUGUHUIUJ
      $.
      $( [19-Dec-2007] $) $( [8-Sep-2007] $)
  $}


