$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Standard axioms for a complex Hilbert space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    hlex.1 $e |- X = ( BaseSet ` U ) $.
    $( The base set of a Hilbert space is a set. $)
    hlex $p |- X e. _V $=
      ( cba cfv cvv fvex eqeltri ) BADEFCADGH $.
      $( [7-Sep-2007] $)
  $}

  ${
    hladdf.1 $e |- X = ( BaseSet ` U ) $.
    hladdf.2 $e |- G = ( +v ` U ) $.
    $( Mapping for Hilbert space vector addition. $)
    hladdf $p |- ( U e. CHil -> G : ( X X. X ) --> X ) $=
      ( chl wcel cnv cxp wf hlnv nvgf syl ) AFGAHGCCICBJAKABCDELM $.
      $( [7-Sep-2007] $)

    $( Hilbert space vector addition is commutative. $)
    hlcom $p |- ( ( U e. CHil /\ A e. X /\ B e. X ) ->
                ( A G B ) = ( B G A ) ) $=
      ( chl wcel cnv co wceq hlnv nvcom syl3an1 ) CHICJIAEIBEIABDKBADKLCMABCDEF
      GNO $.
      $( [7-Sep-2007] $)

    $( Hilbert space vector addition is associative. $)
    hlass $p |- ( ( U e. CHil /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                   ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( chl wcel cnv w3a co wceq hlnv nvass sylan ) DIJDKJAFJBFJCFJLABEMCEMABCE
      MEMNDOABCDEFGHPQ $.
      $( [7-Sep-2007] $)
  $}

  ${
    hl0cl.1 $e |- X = ( BaseSet ` U ) $.
    hl0cl.5 $e |- Z = ( 0vec ` U ) $.
    $( The Hilbert space zero vector. $)
    hl0cl $p |- ( U e. CHil -> Z e. X ) $=
      ( chl wcel cnv hlnv nvzcl syl ) AFGAHGCBGAIABCDEJK $.
      $( [7-Sep-2007] $)
  $}

  ${
    hladdid.1 $e |- X = ( BaseSet ` U ) $.
    hladdid.2 $e |- G = ( +v ` U ) $.
    hladdid.5 $e |- Z = ( 0vec ` U ) $.
    $( Hilbert space addition with the zero vector. $)
    hladdid $p |- ( ( U e. CHil /\ A e. X ) -> ( A G Z ) = A ) $=
      ( chl wcel cnv co wceq hlnv nv0rid sylan ) BIJBKJADJAECLAMBNABCDEFGHOP $.
      $( [7-Sep-2007] $)
  $}

  ${
    hlmulf.1 $e |- X = ( BaseSet ` U ) $.
    hlmulf.4 $e |- S = ( .s ` U ) $.
    $( Mapping for Hilbert space scalar multiplication. $)
    hlmulf $p |- ( U e. CHil -> S : ( CC X. X ) --> X ) $=
      ( chl wcel cnv cc cxp wf hlnv nvsf syl ) BFGBHGICJCAKBLABCDEMN $.
      $( [7-Sep-2007] $)

    $( Hilbert space scalar multiplication by one. $)
    hlmulid $p |- ( ( U e. CHil /\ A e. X ) -> ( 1 S A ) = A ) $=
      ( chl wcel cnv c1 co wceq hlnv nvsid sylan ) CGHCIHADHJABKALCMABCDEFNO $.
      $( [7-Sep-2007] $)

    $( Hilbert space scalar multiplication associative law. $)
    hlmulass $p |- ( ( U e. CHil /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
                   ( ( A x. B ) S C ) = ( A S ( B S C ) ) ) $=
      ( chl wcel cnv cc w3a cmul co wceq hlnv nvsass sylan ) EIJEKJALJBLJCFJMAB
      NOCDOABCDODOPEQABCDEFGHRS $.
      $( [7-Sep-2007] $)
  $}

  ${
    hldi.1 $e |- X = ( BaseSet ` U ) $.
    hldi.2 $e |- G = ( +v ` U ) $.
    hldi.4 $e |- S = ( .s ` U ) $.
    $( Hilbert space scalar multiplication distributive law. $)
    hldi $p |- ( ( U e. CHil /\ ( A e. CC /\ B e. X /\ C e. X ) ) ->
                  ( A S ( B G C ) ) = ( ( A S B ) G ( A S C ) ) ) $=
      ( chl wcel cnv cc w3a co wceq hlnv nvdi sylan ) EKLEMLANLBGLCGLOABCFPDPAB
      DPACDPFPQERABCDEFGHIJST $.
      $( [7-Sep-2007] $)

    $( Hilbert space scalar multiplication distributive law. $)
    hldir $p |- ( ( U e. CHil /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
                  ( ( A + B ) S C ) = ( ( A S C ) G ( B S C ) ) ) $=
      ( chl wcel cnv cc w3a caddc co wceq hlnv nvdir sylan ) EKLEMLANLBNLCGLOAB
      PQCDQACDQBCDQFQRESABCDEFGHIJTUA $.
      $( [7-Sep-2007] $)
  $}

  ${
    hlmul0.1 $e |- X = ( BaseSet ` U ) $.
    hlmul0.4 $e |- S = ( .s ` U ) $.
    hlmul0.5 $e |- Z = ( 0vec ` U ) $.
    $( Hilbert space scalar multiplication by zero. $)
    hlmul0 $p |- ( ( U e. CHil /\ A e. X ) -> ( 0 S A ) = Z ) $=
      ( chl wcel cnv cc0 co wceq hlnv nv0 sylan ) CIJCKJADJLABMENCOABCDEFGHPQ
      $.
      $( [7-Sep-2007] $)
  $}

  ${
    hlipf.1 $e |- X = ( BaseSet ` U ) $.
    hlipf.7 $e |- P = ( .i ` U ) $.
    $( Mapping for Hilbert space inner product. $)
    hlipf $p |- ( U e. CHil -> P : ( X X. X ) --> CC ) $=
      ( chl wcel cnv cxp cc wf hlnv ipf syl ) BFGBHGCCIJAKBLABCDEMN $.
      $( [19-Nov-2007] $)

    $( Conjugate law for Hilbert space inner product. $)
    hlipcj $p |- ( ( U e. CHil /\ A e. X /\ B e. X ) ->
                  ( A P B ) = ( * ` ( B P A ) ) ) $=
      ( chl wcel w3a co ccj cfv wceq cnv hlnv ipcj syl3an1 3com23 eqcomd ) DHIZ
      AEIZBEIZJBACKLMZABCKZUAUCUBUDUENZUADOIUCUBUFDPBACDEFGQRST $.
      $( [8-Sep-2007] $)
  $}

  ${
    hlipdir.1 $e |- X = ( BaseSet ` U ) $.
    hlipdir.2 $e |- G = ( +v ` U ) $.
    hlipdir.7 $e |- P = ( .i ` U ) $.
    $( Distributive law for Hilbert space inner product. $)
    hlipdir $p |- ( ( U e. CHil /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( A G B ) P C ) = ( ( A P C ) + ( B P C ) ) ) $=
      ( chl wcel cphl w3a co caddc wceq hlph ipdir sylan ) EKLEMLAGLBGLCGLNABFO
      CDOACDOBCDOPOQERABCDEFGHIJST $.
      $( [8-Sep-2007] $)
  $}

  ${
    hlipass.1 $e |- X = ( BaseSet ` U ) $.
    hlipass.4 $e |- S = ( .s ` U ) $.
    hlipass.7 $e |- P = ( .i ` U ) $.
    $( Associative law for Hilbert space inner product. $)
    hlipass $p |- ( ( U e. CHil /\ ( A e. CC /\ B e. X /\ C e. X ) ) ->
                  ( ( A S B ) P C ) = ( A x. ( B P C ) ) ) $=
      ( chl wcel cphl cc w3a co cmul wceq hlph ipass sylan ) FKLFMLANLBGLCGLOAB
      EPCDPABCDPQPRFSABCDEFGHIJTUA $.
      $( [8-Sep-2007] $)
  $}

  ${
    hlipgt0.1 $e |- X = ( BaseSet ` U ) $.
    hlipgt0.5 $e |- Z = ( 0vec ` U ) $.
    hlipgt0.7 $e |- P = ( .i ` U ) $.
    $( The inner product of a Hilbert space vector by itself is positive. $)
    hlipgt0 $p |- ( ( U e. CHil /\ A e. X /\ A =/= Z ) -> 0 < ( A P A ) ) $=
      ( chl wcel cnv wne cc0 co clt wbr hlnv w3a cnm cfv c2 cexp cr eqid nvcl
      3adant3 wa wceq nvz biimpd necon3d 3impia sqgt0 syl11anc ipidsq breqtrrd
      syl3an1 ) CIJCKJZADJZAELZMAABNZOPCQURUSUTRZMACSTZTZUAUBNZVAOVBVDUCJZVDMLZ
      MVEOPURUSVFUTACVCDFVCUDZUEUFURUSUTVGURUSUGZVDMAEVIVDMUHAEUHACVCDEFGVHUIUJ
      UKULVDUMUNURUSVAVEUHUTABCVCDFVHHUOUFUPUQ $.
      $( [8-Sep-2007] $)
  $}

  ${
    $d x D $.  $d x F $.  $d x X $.
    hlcompl.1 $e |- X = ( BaseSet ` U ) $.
    hlcompl.8 $e |- D = ( IndMet ` U ) $.
    $( Completeness of a Hilbert space. $)
    hlcompl $p |- ( ( U e. CHil /\ F e. ( Cau ` D ) ) ->
                  E. x e. X F ( ~~>m ` D ) x ) $=
      ( chl wcel cca cfv wa cv clm wbr wrex cdm cms hlcms eqid cmscvg sylan wb
      cnv hlnv imsba rexeqdv syl adantr mpbird ) CHIZDBJKIZLDAMBNKOZAEPZUMABQQZ
      PZUKBRIULUPBCGSABDUOUOTUAUBUKUNUPUCZULUKCUDIZUQCUEURUMAEUOBCEFGUFUGUHUIUJ
      $.
      $( [8-Sep-2007] $)
  $}


