$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Hellinger-Toeplitz Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w y F $.  $d w x y z K $.  $d w x y z N $.  $d w z P $.  $d w x y z W $.
    $d w x y z ph $.  $d u v w x y z T $.  $d u v w x y z U $.  $d w x y z X $.
    htth.1 $e |- X = ( BaseSet ` U ) $.
    htth.2 $e |- P = ( .i ` U ) $.
    htth.3 $e |- L = ( U LnOp U ) $.
    htth.4 $e |- B = ( U BLnOp U ) $.
    ${
      htthlem.5 $e |- N = ( norm ` U ) $.
      htthlem.6 $e |- U e. CHil $.
      htthlem.7 $e |- W = <. <. + , x. >. , abs >. $.
      htthlem.8 $e |- ( ph -> T e. L ) $.
      htthlem.9 $e |- ( ph -> A. x e. X A. y e. X
                        ( x P ( T ` y ) ) = ( ( T ` x ) P y ) ) $.
      htthlem.10 $e |- F = ( z e. X |-> ( w e. X |-> ( w P ( T ` z ) ) ) ) $.
      htthlem.11 $e |- K = ( F " { z e. X | ( N ` z ) <_ 1 } ) $.
      $( Lemma for ~ htth .  The collection ` K ` , which consists of functions
         ` F ( z ) ( w ) = <. w | T ( z ) >. = <. T ( w ) | z >. ` for each
         ` z ` in the unit ball, is a collection of bounded linear functions by
         ~ ipblnfi , so by the Uniform Boundedness theorem ~ ubth , there is a
         uniform bound ` y ` on ` || F ( x ) || ` for all ` x ` in the unit
         ball.  Then
         ` | T ( x ) | ^ 2 = <. T ( x ) | T ( x ) >. = F ( x ) ( T ( x ) ) <_ y
          | T ( x ) | ` , so ` | T ( x ) | <_ y ` and ` T ` is bounded.  (The
         proof was shortened by Mario Carneiro, 12-Jan-2014.) $)
      htthlem $p |- ( ph -> T e. B ) $=
        ( wcel cnmo co cfv cpnf clt wbr cv cle wral cr wrex cabs wa cnv cbn chl
        hlbn ax-mp bnnv wf lnof mp3an12 ffvelrn sylan nvcl sylancr wceq c1 crab
        syl wfun cima cblo cmpt cphl hlph eqid ralrimiva fmpt sylib ffun adantr
        ipblnfi id syl6eleq fvelima syl2an ex fveq2 breq1d elrab fveq2d opreq2d
        simpl mpteq2dva cba cvv fvex eqeltri mptex fvmpt fveq1d oprex sylan9eqr
        opreq1 ad2ant2lr wi adantrr eqtrd cmul cc ax-mulrcl syl11anc cc0 simprr
        a1i jca syl31anc recnd letrd syl5ibcom rexlimdva syld wb mpbid ad2ant2r
        wss mpd 0re adantl 3expia expr cxr ra42 imp anassrs ipcl abscl ad2antrl
        mp3an1 mpan sii 1re nvge0 lemul2a mulid1 breqtrd eqbrtrd fveq1 ralrimiv
        sylan2b ralbidv rcla4ev crn imassrn eqsstri frn syl5ss cnnv cnnvnm ubth
        breq2 sylibr cdm fdm eleq2d biimpar funfvima syldan syl6eleqr rcla4v wo
        simpr leloe cnnvba nmblore simplr cexp ipidsq resqcl sqge0 absid 3eqtrd
        c2 sqval nmblolbi eqbrtrrd lemul1a lemul1 biimprd expdimp syl21anc mpid
        blof nmoge0 breq1 jaod com23 ralrimdva rexr nmoubi ltpnf nmoxr xrlelttr
        w3a pnfxr mp3an3 mpan2d 3syld isblo mp2an sylanbrc ) AHLUGZHIIUHUIZUJZU
        KULUMZHFUGZUCAEUNZINUHUIZUJZCUNZUOUMZEKUPZCUQURZUYCABUNZUYEUJZUSUJZDUNZ
        UOUMZEKUPZDUQURZBOUPZUYKAUYRBOAUYLOUGZUTZUYLHUJZMUJZUQUGZUYNVUCUOUMZEKU
        PZUYRVUAIVAUGZVUBOUGZVUDIVBUGZVUGIVCUGZVUIUAIVDVEZIVFVEZAOOHVGZUYTVUHAU
        XTVUMUCVUGVUGUXTVUMVULVULHILIOOPPRVHVIVQZOOUYLHVJVKZVUBIMOPTVLVMZVUAVUE
        EKVUAUYEKUGZUYHJUJZUYEVNZCUYOMUJZVOUOUMZDOVPZURZVUEVUAVUQVVCVUAJVRZUYEJ
        VVBVSZUGVVCVUQAVVDUYTAOINVTUIZJVGZVVDAEOUYEUYOHUJZGUIZWAZVVFUGZDOUPVVGA
        VVKDOAUYOOUGZUTVVHOUGZVVKAVUMVVLVVMVUNOOUYOHVJVKEVVHVVFNGIVVJOPQVUJIWBU
        GUAIWCVEZUBVVFWDZVVJWDWJVQWEDOVVFVVJJUEWFWGZOVVFJWHVQZWIVUQUYEKVVEVUQWK
        UFWLCUYEVVBJWMWNWOVUAVUSVUECVVBVUAUYHVVBUGZUTUYLVURUJZUSUJZVUCUOUMZVUSV
        UEVVRVUAUYHOUGZUYHMUJZVOUOUMZUTZVWAVVAVWDDUYHOUYOUYHVNZVUTVWCVOUOUYOUYH
        MWPWQWRVUAVWEUTZVVTVUBUYHGUIZUSUJZVUCUOVWGVVSVWHUSVWGVVSUYLUYHHUJZGUIZV
        WHUYTVWBVVSVWKVNAVWDVWBUYTVVSUYLEOUYEVWJGUIZWAZUJVWKVWBUYLVURVWMDUYHVVJ
        VWMOJVWFEOVVIVWLVWFUYEOUGZUTZVVHVWJUYEGVWOUYOUYHHVWFVWNXAWSWTXBUEEOVWLO
        IXCUJXDPIXCXEXFZXGXHXIEUYLVWLVWKOVWMUYEUYLVWJGXLVWMWDUYLVWJGXJXHXKXMVUA
        VWBVWKVWHVNZVWDAUYTVWBVWQAUYTVWBUTZVWQAVWQCOUPBOUPVWRVWQXNUDVWQBCOOUUAV
        QUUBUUCXOXPWSVWGVWIVUCVWCXQUIZVUCVWGVWHXRUGZVWIUQUGVUAVUHVWBVWTVWEVUOVW
        BVWDXAZVUGVUHVWBVWTVULVUBUYHGIOPQUUDUUGWNVWHUUEVQVWGVUDVWCUQUGZVWSUQUGV
        UAVUDVWEVUPWIZVWBVXBVUAVWDVUGVWBVXBVULUYHIMOPTVLUUHUUFZVUCVWCXSXTVXCVUA
        VUHVWBVWIVWSUOUMVWEVUOVXAVUBUYHGIMOPTQVVNUUIWNVWGVWSVUCVOXQUIZVUCUOVWGV
        XBVOUQUGZVUDYAVUCUOUMZUTZVWDVWSVXEUOUMVXDVXFVWGUUJYCVUAVXHVWEVUAVUDVXGV
        UPVUAVUGVUHVXGVULVUOVUBIMOPTUUKZVMYDWIVUAVWBVWDYBVWCVOVUCUULYEVWGVUCXRU
        GZVXEVUCVNVWGVUCVXCYFVUCUUMVQUUNYGUUOUURVUSVVTUYNVUCUOVUSVVSUYMUSUYLVUR
        UYEUUPWSWQYHYIYJUUQUYQVUFDVUCUQUYOVUCVNUYPVUEEKUYOVUCUYNUOUVIUUSUUTXTWE
        AKVVFYNZUYSUYKYKZAKJUVAZVVFKVVEVXMUFJVVBUVBUVCAVVGVXMVVFYNVVPOVVFJUVDVQ
        UVEVUINVAUGZVXKVXLVUKNUBUVFZBEKIUYFUSNODCPNUBUVGZUYFWDZUVHVIVQYLAUYJUYC
        CUQAUYHUQUGZUTZUYJUYLMUJZVOUOUMZVUCUYHUOUMZXNZBOUPZUYBUYHUOUMZUYCVXSUYJ
        VYCBOVXSUYTUTVYAUYJVYBVXSUYTVYAUYJVYBXNVXSUYTVYAUTZUTZUYJUYLJUJZUYFUJZU
        YHUOUMZVYBVYGVYHKUGUYJVYJXNVYGVYHVVEKVYGUYLVVBUGZVYHVVEUGZVYGVYFVYKVXSV
        YFUVTVVAVYADUYLOUYOUYLVNZVUTVXTVOUOUYOUYLMWPWQWRUVJAUYTVYKVYLXNZVXRVYAA
        UYTUYLJUVKZUGZVYNAVYPUYTAVYOOUYLAVVGVYOOVNVVPOVVFJUVLVQUVMUVNAVVDVYPVYN
        VVQVVBUYLJUVOVKUVPYMYOUFUVQUYIVYJEVYHKUYEVYHVNUYGVYIUYHUOUYEVYHUYFWPWQU
        VRVQVXSUYTVYJVYBXNVYAVXSUYTVYJVYBVXSUYTVYJUTZUTZYAVUCULUMZYAVUCVNZUVSZV
        YBVYRVXGWUAVYRVUGVUHVXGVULAUYTVUHVXRVYJVUOYMZVXIVMZVYRYAUQUGZVUDVXGWUAY
        KYPAUYTVUDVXRVYJVUPYMZYAVUCUWAVMYLVYRVYSVYBVYTVYRVYSVUCVUCXQUIZUYHVUCXQ
        UIZUOUMZVYBVYRWUFVYIVUCXQUIZWUGVYRVUDVUDWUFUQUGWUEWUEVUCVUCXSXTVYRVYIUQ
        UGZVUDWUIUQUGAUYTWUJVXRVYJVUAVYHVVFUGZWUJAVVGUYTWUKVVPOVVFUYLJVJVKZVUGV
        XNWUKWUJVULVXOVVFVYHIUYFNOXRPNUBUWBZVXQVVOUWCVIVQYMZWUEVYIVUCXSXTVYRVXR
        VUDWUGUQUGAVXRVYQUWDZWUEUYHVUCXSXTVYRVUBVYHUJZUSUJZWUFWUIUOVYRWUQVUCUWK
        UWEUIZUSUJZWURWUFVYRWUPWURUSVYRWUPVUBVUBGUIZWURAUYTWUPWUTVNVXRVYJVUAWUP
        VUBEOUYEVUBGUIZWAZUJZWUTVUAVUBVYHWVBUYTVYHWVBVNADUYLVVJWVBOJVYMEOVVIWVA
        VYMVWNUTZVVHVUBUYEGWVDUYOUYLHVYMVWNXAWSWTXBUEEOWVAVWPXGXHYQXIVUAVUHWVCW
        UTVNVUOEVUBWVAWUTOWVBUYEVUBVUBGXLWVBWDVUBVUBGXJXHVQXPYMVYRVUGVUHWUTWURV
        NVULWUBVUBGIMOPTQUWFVMXPWSVYRVUDWUSWURVNZWUEVUDWURUQUGYAWURUOUMWVEVUCUW
        GVUCUWHWURUWIXTVQVYRVXJWURWUFVNVYRVUCWUEYFVUCUWLVQUWJVYRWUKVUHWUQWUIUOU
        MAUYTWUKVXRVYJWULYMWUBVUBVVFVYHIMUSUYFNOPTVXPVXQVVOVULVXOUWMXTUWNVYRWUJ
        VXRVXHVYJWUIWUGUOUMWUNWUOVYRVUDVXGWUEWUCYDVXSUYTVYJYBZVYIUYHVUCUWOYEYGV
        YRVUDVXRVUDVYSWUHVYBXNZXNWUEWUOWUEVUDVXRUTVUDVYSWVGVUDVXRVUDVYSUTZWVGVU
        DVXRWVHUXLVYBWUHVUCUYHVUCUWPUWQYRUWRUWSUWTVYRYAUYHUOUMVYTVYBVYRYAVYIUYH
        WUDVYRYPYCWUNWUOVYROXRVYHVGZYAVYIUOUMZAUYTWVIVXRVYJVUAWUKWVIWULVUGVXNWU
        KWVIVULVXOVVFVYHINOXRPWUMVVOUXAVIVQYMVUGVXNWVIWVJVULVXOVYHIUYFNOXRPWUMV
        XQUXBVIVQWVFYGYAVUCUYHUOUXCYHUXDYOYSXOYJYSUXEUXFAVUMUYHYTUGZVYDVYEXNVXR
        VUNUYHUXGZVUMWVKVYDVYEBUYHHIMMUYAIOOPPTTUYAWDZVULVULUXHYRWNVXSVYEUYHUKU
        LUMZUYCVXRWVNAUYHUXIYQAUYBYTUGZWVKVYEWVNUTUYCXNZVXRAVUMWVOVUNVUGVUGVUMW
        VOVULVULHIUYAIOOPPWVMUXJVIVQWVLWVOWVKUKYTUGWVPUXMUYBUYHUKUXKUXNWNUXOUXP
        YIYOVUGVUGUYDUXTUYCUTYKVULVULFHILUYAIWVMRSUXQUXRUXS $.
        $( [12-Jan-2014] $) $( [11-Jan-2008] $)
    $}

    $( Hellinger-Toeplitz Theorem: any self-adjoint linear operator defined on
       all of Hilbert space is bounded.  Theorem 10.1-1 of [Kreyszig] p. 525.
       Discovered by E. Hellinger and O. Toeplitz in 1910, "it aroused both
       admiration and puzzlement since the theorem establishes a relation
       between properties of two different kinds, namely, the properties of
       being defined everywhere and being bounded."  (The proof was shortened
       by Mario Carneiro, 12-Jan-2014.) $)
    htth $p |- ( ( U e. CHil /\ T e. L /\
        A. x e. X A. y e. X ( x P ( T ` y ) ) = ( ( T ` x ) P y ) ) ->
      T e. B ) $=
      ( vw wcel cv cfv co wceq wral fveq2 vu vv vz chl wa wi caddc cmul cop cif
      cabs clno cip cblo opreq12 anidms syl5eq eleq2d eqeq12d raleqbidv anbi12d
      cba opreqd imbi12d cmpt cnm c1 cle crab cima eqid cnhl elimel simpl simpr
      wbr opreq1 opreq1d opreq2d opreq2 cbvral2v sylib cbvmptv adantr mpteq2dva
      breq1d cbvrabv imaeq2i htthlem dedth 3impib ) FUDNZEGNZAOZBOZEPZDQZWNEPZW
      ODQZRZBHSZAHSZECNZWLWMXBUEZXCUFEWLFUGUHUIUKUIZUJZXFULQZNZWNWPXFUMPZQZWRWO
      XIQZRZBXFVBPZSZAXMSZUEZEXFXFUNQZNZUFFXEFXFRZXDXPXCXRXSWMXHXBXOXSGXGEXSGFF
      ULQZXGKXSXTXGRFXFFXFULUOUPUQURXSXAXNAHXMXSHFVBPXMIFXFVBTUQZXSWTXLBHXMYAXS
      WQXJWSXKXSDXIWNWPXSDFUMPXIJFXFUMTUQZVCXSDXIWRWOYBVCUSUTUTVAXSCXQEXSCFFUNQ
      ZXQLXSYCXQRFXFFXFUNUOUPUQURVDXPUAUBUCMXQXIEXFAXMBXMWOWRXIQZVEZVEZYFWNXFVF
      PZPZVGVHVPZAXMVIZVJXGYGXEXMXMVKXIVKXGVKXQVKYGVKFXEUDXEXEVKZVLVMYKXHXOVNXP
      XOUAOZUBOZEPZXIQZYLEPZYMXIQZRZUBXMSUAXMSXHXOVOXLYRYLWPXIQZYPWOXIQZRABUAUB
      XMXMWNYLRZXJYSXKYTWNYLWPXIVQUUAWRYPWOXIWNYLETVRUSWOYMRZYSYOYTYQUUBWPYNYLX
      IWOYMETVSWOYMYPXIVTUSWAWBAUCXMYEMXMMOZUCOZEPZXIQZVEZWNUUDRZYEMXMUUCWRXIQZ
      VEUUGBMXMYDUUIWOUUCWRXIVQWCUUHMXMUUIUUFUUHUUIUUFRUUCXMNUUHWRUUEUUCXIWNUUD
      ETVSWDWEUQWCYJUUDYGPZVGVHVPZUCXMVIYFYIUUKAUCXMUUHYHUUJVGVHWNUUDYGTWFWGWHW
      IWJWK $.
      $( [12-Jan-2014] $) $( [11-Jan-2008] $)
  $}

