$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Hellinger-Toeplitz Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    htthlem1.1 $e |- X = ( BaseSet ` U ) $.
    htthlem1.l $e |- L = ( U LnOp U ) $.
    htthlem1.u $e |- U e. CHil $.
    htthlem1.t $e |- T e. L $.
    $( Lemma for ~ htthi .  Closure of values of an operator ` T ` on an
       auxiliary sequence of vectors ` f ` . $)
    htthlem1 $p |- ( ( f : NN --> X /\ k e. NN ) ->
            ( T ` ( f ` k ) ) e. X ) $=
      ( cn cv wf wcel wa cfv ffvelrn cnv hlnvi lnof mp3an ffvelrni syl ) KFCLZM
      DLZKNOUEUDPZFNUFAPFNKFUEUDQFFUFABRNZUGAENFFAMBISZUHJABEBFFGGHTUAUBUC $.
      $( [12-Jan-2008] $)
  $}

  ${
    $d x y A $.  $d x y P $.  $d y Q $.  $d x y T $.  $d x y X $.
    htthlem2.a $e |- ( ( x e. X /\ y e. X ) ->
                  ( ( T ` x ) P y ) = ( x P ( T ` y ) ) ) $.
    $( Lemma for ~ htthi .  Elevate set variables to class variables in the
       self-adjoint hypothesis. $)
    htthlem2 $p |- ( ( A e. X /\ Q e. X ) ->
                  ( ( T ` A ) P Q ) = ( A P ( T ` Q ) ) ) $=
      ( cv cfv co wceq fveq2 opreq1d opreq1 eqeq12d opreq2 opreq2d vtocl2ga )
      AIZFJZBIZDKZTUBFJZDKZLCFJZUBDKZCUDDKZLUFEDKZCEFJZDKZLABCEGGTCLZUCUGUEUHUL
      UAUFUBDTCFMNTCUDDOPUBELZUGUIUHUKUBEUFDQUMUDUJCDUBEFMRPHS $.
      $( [12-Jan-2008] $)
  $}

  ${
    $d c u v x y A $.  $d c d k z C $.  $d c d k z D $.  $d c d k z F $.
    $d c f k z N $.  $d d z O $.  $d m u v w x y P $.  $d c k u v x y Q $.
    $d c f k m u v w x y z T $.  $d c d k u v z U $.
    $d c d f k m u v w x y z X $.  $d d f k m u v w x y z $.
    htthlem3.1 $e |- X = ( BaseSet ` U ) $.
    htthlem3.p $e |- P = ( .i ` U ) $.
    htthlem3.l $e |- L = ( U LnOp U ) $.
    htthlem3.b $e |- B = ( U BLnOp U ) $.
    htthlem3.u $e |- U e. CHil $.
    htthlem3.t $e |- T e. L $.
    htthlem3.a $e |- ( ( x e. X /\ y e. X ) ->
                  ( ( T ` x ) P y ) = ( x P ( T ` y ) ) ) $.
    htthlem3.f $e |- F = { <. m , w >. | ( m e. NN /\ w = { <. v , u >. |
        ( v e. X /\ u = ( ( T ` v ) P ( f ` m ) ) ) } ) } $.
    htthlem3.c $e |- C = <. <. + , x. >. , abs >. $.
    htthlem3.d $e |- D = ( U BLnOp C ) $.
    htthlem3.n $e |- N = ( norm ` U ) $.
    htthlem3.o $e |- O = ( U normOp C ) $.
    $( Lemma for ~ htthi .  Construct an auxiliary sequence of functionals
       ` F `` k ` from inner products of the given function ` T ` and auxiliary
       vector sequence ` f ` . $)
    htthlem3 $p |- ( k e. NN -> ( F ` k ) = { <. v , u >. | ( v e. X /\
        u = ( ( T ` v ) P ( f ` k ) ) ) } ) $=
      ( cv wcel cfv co wa copab cn fveq2 opreq2d eqeq2d anbi2d opabbidv opabex2
      wceq hlex fvopab4 ) NCMULZDULZSUMZEULZVIJUNZNULZLULZUNZIUOZVEZUPZDEUQVJVK
      VLVHVNUNZIUOZVEZUPZDEUQUROVMVHVEZVRWBDEWCVQWAVJWCVPVTVKWCVOVSVLIVMVHVNUSU
      TVAVBVCUGDESVTKSTVFVDVG $.
      $( [11-Jan-2008] $)

    $( Lemma for ~ htthi .  Value of a functional ` F `` k ` . $)
    htthlem4 $p |- ( ( A e. X /\ k e. NN ) -> ( ( F ` k ) ` A ) =
          ( ( T ` A ) P ( f ` k ) ) ) $=
      ( cv cn wcel cfv co wceq copab htthlem3 fveq1d fveq2 opreq1d eqid fvopab4
      wa oprex sylan9eqr ) NUMZUNUOZFTUOFVIPUPZUPFDUMZTUOEUMVLKUPZVIMUMUPZJUQZU
      RVFDEUSZUPFKUPZVNJUQZVJFVKVPABCDEGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUT
      VADEFVOVRTVPVLFURVMVQVNJVLFKVBVCVPVDVQVNJVGVEVH $.
      $( [12-Jan-2008] $)

    $( Lemma for ~ htthi .  Each ` F `` k ` is a bounded linear functional
       (i.e. a bounded linear operator from the vector space to ` CC ` ). $)
    htthlem5 $p |- ( ( f : NN --> X /\ k e. NN ) -> ( F ` k ) e. D ) $=
      ( cn cv wf wcel wa cfv wceq copab htthlem3 adantl ffvelrn htthlem2 ancoms
      co sylan eqeq2d pm5.32da opabbidv eqtrd htthlem1 chl cphl hlph ax-mp cmpt
      df-mpt eqcomi ipblnfi syl eqeltrd ) ULSLUMZUNZMUMZULUOZUPZWDOUQZDUMZSUOZE
      UMZWHWDWBUQZJUQZIVEZURZUPZDEUSZHWFWGWIWJWHJUQWKIVEZURZUPZDEUSZWPWEWGWTURW
      CABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKUTVAWFWSWODEWFWIWRWNWFWIUPWQWM
      WJWFWKSUOZWIWQWMURZULSWDWBVBWIXAXBABWHIWKJSUFVCVDVFVGVHVIVJWFWLSUOWPHUOJK
      LMPSTUBUDUEVKDWLHGIKWPSTUAKVLUOKVMUOUDKVNVOUHUIDSWMVPWPDESWMVQVRVSVTWA $.
      $( [19-Nov-2013] $) $( [13-Jan-2008] $)

    $( Lemma for ~ htthi .  An upper bound of all ` F `` k ` at a given vector
       ` A ` , when the norms of auxiliary vector sequence ` f ` are all 1 or
       less. $)
    htthlem6 $p |- ( ( ( A e. X /\ ( f : NN --> X /\ k e. NN ) ) /\ ( N ` (
    f ` k ) ) <_ 1 ) -> ( abs ` ( ( F ` k ) ` A ) ) <_ ( N ` ( T ` A ) ) ) $=
      ( wcel cn cv wf wa cfv c1 cle cabs co wceq htthlem4 fveq2d adantrl adantr
      wbr cnv hlnvi lnof mp3an ffvelrni ffvelrn anim12i cmul cr cc mp3an1 abscl
      ipcl syl nvcl mpan remulcl ad2antrr chl cphl hlph ax-mp sii cc0 nvge0 jca
      syl2an ancoms lemul2a mp3anl2 sylan recnd mulid1 breqtrd letrd eqbrtrd
      1re ) FTUMZUNTMUOZUPZNUOZUNUMZUQZUQZXIXGURZRURZUSUTVHZUQFXIPURURZVAURZFKU
      RZXMJVBZVAURZXRRURZUTXLXQXTVCZXOXFXJYBXHXFXJUQXPXSVAABCDEFGHIJKLMNOPQRSTU
      AUBUCUDUEUFUGUHUIUJUKULVDVEVFVGXLXRTUMZXMTUMZUQZXOXTYAUTVHXFYCXKYDTTFKLVI
      UMZYFKQUMTTKUPLUEVJZYGUFKLQLTTUAUAUCVKVLVMUNTXIXGVNVOYEXOUQZXTYAXNVPVBZYA
      YEXTVQUMZXOYEXSVRUMZYJYFYCYDYKYGXRXMJLTUAUBWAVSXSVTWBVGYEYIVQUMZXOYCYAVQU
      MZXNVQUMZYLYDYFYCYMYGXRLRTUAUKWCWDZYFYDYNYGXMLRTUAUKWCWDZYAXNWEWOVGYCYMYD
      XOYOWFYEXTYIUTVHXOXRXMJLRTUAUKUBLWGUMLWHUMUELWIWJWKVGYHYIYAUSVPVBZYAUTYEY
      NYMWLYAUTVHZUQZUQZXOYIYQUTVHZYDYCYTYDYNYCYSYPYCYMYRYOYFYCYRYGXRLRTUAUKWMW
      DWNVOWPYNUSVQUMYSXOUUAXEXNUSYAWQWRWSYCYQYAVCZYDXOYCYAVRUMUUBYCYAYOWTYAXAW
      BWFXBXCWSXD $.
      $( [16-Jan-2008] $)

    $( Lemma for ~ htthi .  Convert upper bound in ~ htthlem6 to an existence
       condition. $)
    htthlem7 $p |- ( ( ( Q e. X /\ f : NN --> X ) /\ A. k e. NN ( N ` ( f `
    k ) ) <_ 1 ) -> E. c e. RR A. k e. NN ( abs ` ( ( F ` k ) ` Q ) ) <_ c ) $=
      ( wcel cn cv wf wa cfv c1 cle wbr wral cabs wrex cnv hlnvi mp3an ffvelrni
      cr lnof nvcl mpan syl ad2antrr wi htthlem6 ex anassrs ralimdva wceq breq2
      imp ralbidv rcla4ev syl11anc ) JTUNZUOTMUPZUQZURZNUPZWHUSRUSUTVAVBZNUOVCZ
      URJKUSZRUSZVJUNZJWKPUSUSVDUSZWOVAVBZNUOVCZWQUAUPZVAVBZNUOVCZUAVJVEWGWPWIW
      MWGWNTUNZWPTTJKLVFUNZXDKQUNTTKUQLUFVGZXEUGKLQLTTUBUBUDVKVHVIXDXCWPXEWNLRT
      UBULVLVMVNVOWJWMWSWJWLWRNUOWGWIWKUOUNZWLWRVPWGWIXFURURWLWRABCDEJFGHIKLMNO
      PQRSTUBUCUDUEUFUGUHUIUJUKULUMVQVRVSVTWCXBWSUAWOVJWTWOWAXAWRNUOWTWOWQVAWBW
      DWEWF $.
      $( [16-Jan-2008] $)

    $( Lemma for ~ htthi . $)
    htthlem8 $p |- ( ( ( ( F ` k ) e. D /\ d e. RR /\ A e. X ) /\ ( O ` ( F
       ` k ) ) <_ d ) -> ( abs ` ( ( F ` k ) ` A ) ) <_ ( d x. ( N ` A ) ) ) $=
      ( cv cfv wcel cr w3a cle wbr wa cabs cmul co cc wf hlnvi cnnv cnnvba blof
      cnv mp3an12 ffvelrn sylan abscl syl 3adant2 adantr nmblore remulcl syl2an
      nvcl mpan sylan2 3adant1 cnnvnm nmblolbi cc0 id nvge0 jca lemul1a syl3anl
      letrd ) NUNPUOZIUPZUAUNZUQUPZFTUPZURZWOSUOZWQUSUTZVAFWOUOZVBUOZXAFRUOZVCV
      DZWQXEVCVDZWTXDUQUPZXBWPWSXHWRWPWSVAXCVEUPZXHWPTVEWOVFZWSXILVKUPZHVKUPZWP
      XJLUFVGZHUJVHZIWOLHTVEUBHUJVIZUKVJVLTVEFWOVMVNXCVOVPVQVRWTXFUQUPZXBWPWSXP
      WRWPXAUQUPZXEUQUPZXPWSXKXLWPXQXMXNIWOLSHTVEUBXOUMUKVSVLZXKWSXRXMFLRTUBULW
      BWCZXAXEVTWAVQVRWTXGUQUPZXBWRWSYAWPWSWRXRYAXTWQXEVTWDWEVRWTXDXFUSUTZXBWPW
      SYBWRFIWOLRVBSHTUBULHUJWFUMUKXMXNWGVQVRWPXQWRWRWSXRWHXEUSUTZVAXBXFXGUSUTX
      SWRWIWSXRYCXTXKWSYCXMFLRTUBULWJWCWKXAWQXEWLWMWN $.
      $( [17-Jan-2008] $)

    $( Lemma for ~ htthi . $)
    htthlem9 $p |- ( ( f : NN --> X /\ k e. NN ) -> ( ( N ` ( T ` ( f `
           k ) ) ) ^ 2 ) = ( abs ` ( ( F ` k ) ` ( T ` ( f ` k ) ) ) ) ) $=
      ( cn cv wf wcel wa cfv c2 cexp co cabs wceq htthlem1 cr cc0 cle wbr hlnvi
      cnv nvcl mpan resqcl syl sqge0 syl11anc htthlem4 adantrl ffvelrn htthlem2
      absid sylan2 eqtrd mpancom ipidsq sylancr eqtr2d fveq2d eqtr3d ) ULSLUMZU
      NZMUMZULUOZUPZWKWIUQZJUQZQUQZURUSUTZVAUQZWQWOWKOUQUQZVAUQWMWOSUOZWRWQVBZJ
      KLMPSTUBUDUEVCZWTWQVDUOZVEWQVFVGZXAWTWPVDUOZXCKVIUOZWTXEKUDVHZWOKQSTUJVJV
      KZWPVLVMWTXEXDXHWPVNVMWQVTVOVMWMWQWSVAWMWSWOWOIUTZWQWTWMWSXIVBXBWTWMUPWSW
      OJUQWNIUTZXIWTWLWSXJVBWJABCDEWOFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKVPVQW
      MWTWNSUOXJXIVBULSWKWIVRABWOIWNJSUFVSWAWBWCWMXFWTXIWQVBXGXBWOIKQSTUJUAWDWE
      WFWGWH $.
      $( [18-Jan-2008] $)

    $( Look at reworking triple conj in htthlem8(?) to shorten this? $)
    $( Lemma for ~ htthi . $)
    htthlem10 $p |- ( ( ( f : NN --> X /\ k e. NN ) /\ ( d e. RR /\
        ( O ` ( F ` k ) ) <_ d ) ) -> ( N ` ( T ` ( f ` k ) ) ) <_ d ) $=
      ( cn cv wf wcel wa cr cfv cle wbr cexp cmul cabs wceq htthlem9 adantr w3a
      c2 co htthlem5 simpr htthlem1 3jca htthlem8 sylan anasss eqbrtrd wb hlnvi
      cc0 cnv nvcl sylancr nvge0 simprl cnnv cnnvba blof mp3an12 nmoge0 nmblore
      cc syl wi 0re letr mp3an1 mpand impr sqlecan syl22anc mpbid ) UMSLUNZUOMU
      NZUMUPUQZTUNZURUPZXEOUSZRUSZXGUTVAZUQZUQZXEXDUSJUSZQUSZVIVBVJZXGXOVCVJZUT
      VAZXOXGUTVAZXMXPXNXIUSVDUSZXQUTXFXPXTVEXLABCDEFGHIJKLMNOPQRSUAUBUCUDUEUFU
      GUHUIUJUKULVFVGXFXHXKXTXQUTVAZXFXHUQZXIHUPZXHXNSUPZVHXKYAYBYCXHYDXFYCXHAB
      CDEFGHIJKLMNOPQRSUAUBUCUDUEUFUGUHUIUJUKULVKZVGXFXHVLXFYDXHJKLMPSUAUCUEUFV
      MZVGVNABCDEXNFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULVOVPVQVRXMXOURUPZWAXO
      UTVAZXHWAXGUTVAZXRXSVSXFYGXLXFKWBUPZYDYGKUEVTZYFXNKQSUAUKWCWDVGXFYHXLXFYJ
      YDYHYKYFXNKQSUAUKWEWDVGXFXHXKWFXFYCXLYIYEYCXHXKYIYCXHUQWAXJUTVAZXKYIYCYLX
      HYCSWMXIUOZYLYJGWBUPZYCYMYKGUIWGZHXIKGSWMUAGUIWHZUJWIWJYJYNYMYLYKYOXIKRGS
      WMUAYPULWKWJWNVGYCXJURUPZXHYLXKUQYIWOZYJYNYCYQYKYOHXIKRGSWMUAYPULUJWLWJWA
      URUPYQXHYRWPWAXJXGWQWRVPWSWTVPXOXGXAXBXC $.
      $( [18-Jan-2008] $)

    $( Lemma for ~ htthi .  Use the Uniform Boundedness Theorem ~ ubthi to show
       that the functional ` F `` k ` is bounded. $)
    htthlem11 $p |- ( ( f : NN --> X /\ A. k e. NN ( N ` ( f ` k ) ) <_ 1 )
          -> E. d e. RR A. k e. NN ( O ` ( F ` k ) ) <_ d ) $=
      ( vz vc cn cv wf cfv c1 cle wbr wral wa cabs cr wrex wfn wcel co wceq cba
      copab cvv fvex eqeltri opabex2 fnopab2 htthlem5 r19.21aiva ffnfv sylanbrc
      a1i adantr htthlem7 ancom1s an32s cnnvnm chl cbn hlbn ax-mp cnnv syl11anc
      ubthi ) UOSLUPZUQZMUPZWOURQURUSUTVAMUOVBZVCZUOHOUQZUMUPZWQOURZURVDURUNUPU
      TVAMUOVBUNVEVFZUMSVBXBRURTUPUTVAMUOVBTVEVFWPWTWRWPOUOVGZXBHVHZMUOVBWTXDWP
      NCUODUPZSVHEUPXFJURNUPWOURIVIZVJVCDEVLODESXGSKVKURVMUAKVKVNVOVPUHVQWBWPXE
      MUOABCDEFGHIJKLMNOPQRSUAUBUCUDUEUFUGUHUIUJUKULVRVSMUOHOVTWAWCWSXCUMSWPXAS
      VHZWRXCXHWPWRXCABCDEFGHIXAJKLMNOPQRSUNUAUBUCUDUEUFUGUHUIUJUKULWDWEWFVSUMH
      OKMVDRGSUNTUAGUIWGULUJKWHVHKWIVHUEKWJWKGUIWLWNWM $.
      $( [16-Jan-2008] $)

    $( Lemma for ~ htthi .  Linear operator ` T ` is bounded. $)
    htthlem12 $p |- T e. B $=
      ( vk vz wcel cnmo co cfv cr cnv wa wb hlnvi eqid isblo2 mp2an wf cn cv c1
      cle wbr wral wrex wi wal lnof mp3an htthlem11 htthlem10 ralimdva reximdva
      expr an32s adantr mpd htthlem1 nvcl mpan syl anim1i reximdv bndndx ax-gen
      ex pm3.2i nmobndseqi ax-mp mpbir2an ) JFUMZJOUMZJKKUNUOZUPUQUMZKURUMZXBWR
      WSXAUSUTKUCVAZXCFJKOWTKWTVBZUAUBVCVDUDRRJVEZVFRLVGZVEZUKVGZXFUPZPUPVHVIVJ
      UKVFVKZUSZXIJUPZPUPZXHVIVJUKVFVLZVMZLVNZUSXAXEXPXBXBWSXEXCXCUDJKOKRRSSUAV
      OVPXOLXKXMUQUMZXMULVGZVIVJZUSZUKVFVKZULUQVLZXNXKXSUKVFVKZULUQVLZYBXKXHNUP
      QUPXRVIVJZUKVFVKZULUQVLZYDABCDEFGHIJKLUKMNOPQRULSTUAUBUCUDUEUFUGUHUIUJVQX
      GYGYDVMXJXGYFYCULUQXGXRUQUMZUSYEXSUKVFXGXHVFUMZYHYEXSVMXGYIUSZYHYEXSABCDE
      FGHIJKLUKMNOPQRULSTUAUBUCUDUEUFUGUHUIUJVRWAWBVSVTWCWDXGYDYBVMXJXGYCYAULUQ
      XGXSXTUKVFYJXSXTYJXQXSYJXLRUMZXQJKLUKORSUAUCUDWEXBYKXQXCXLKPRSUIWFWGWHWIW
      MVSWJWCWDULXMUKWKWHWLWNJKLUKPPWTKRRSSUIUIXDXCXCWOWPWQ $.
      $( [18-Jan-2008] $)
  $}

  ${
    $d m u v w x y P $.  $d f m u v w x y T $.  $d f u v U $.
    $d f m u v w x y X $.
    htthi.1 $e |- X = ( BaseSet ` U ) $.
    htthi.p $e |- P = ( .i ` U ) $.
    htthi.l $e |- L = ( U LnOp U ) $.
    htthi.b $e |- B = ( U BLnOp U ) $.
    htthi.u $e |- U e. CHil $.
    htthi.t $e |- T e. L $.
    htthi.a $e |- ( ( x e. X /\ y e. X ) ->
                  ( ( T ` x ) P y ) = ( x P ( T ` y ) ) ) $.
    $( Hellinger-Toeplitz Theorem: any self-adjoint linear operator defined on
       all of Hilbert space is bounded.  Theorem 10.1-1 of [Kreyszig] p. 525.
       Discovered by E. Hellinger and O. Toeplitz in 1910, "it aroused both
       admiration and puzzlement since the theorem establishes a relation
       between properties of two different kinds, namely, the properties of
       being defined everywhere and being bounded." $)
    htthi $p |- T e. B $=
      ( vw vv vu cv eqid vf vm caddc cmul cop cabs cblo co cn wcel cfv wa copab
      wceq cnm cnmo htthlem12 ) ABPQRCUCUDUEUFUEZFURUGUHZDEFUAUBUBSZUIUJPSQSZHU
      JRSVAEUKUTUASUKDUHUNULQRUMUNULUBPUMZGFUOUKZFURUPUHZHIJKLMNOVBTURTUSTVCTVD
      TUQ $.
      $( [11-Jan-2008] $)
  $}

