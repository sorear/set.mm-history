$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CHil $.

  $( Extend class notation with the class of all complex Hilbert spaces. $)
  chl $a class CHil $.

  ${
    $d g n s w x y $.
    $( Define the class of all complex Hilbert spaces.  A Hilbert space is a
       Banach space which is also an inner product space. $)
    df-hl $a |- CHil = ( CBan i^i CPreHil ) $.

    $( Define the class of all complex Hilbert spaces.  A Hilbert space is a
       Banach space whose norm satisfies the parallelogram law.  Based on
       Exercise 4(b) of [ReedSimon] p. 63.  The vector operation is ` g ` , the
       scalar product is ` s ` , and the norm is ` n ` . $)
    dfhlOLD $p |- CHil = ( CBan i^i { <. <. g , s >. , n >. |
          A. x e. ran g A. y e. ran g
       ( ( ( n ` ( x g y ) ) ^ 2 ) + ( ( n ` ( x g ( -u 1 s y ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( n ` x ) ^ 2 ) + ( ( n ` y ) ^ 2 ) ) ) } ) $=
      ( chl cbn cphl cin cv co cfv c2 cexp c1 cneg caddc cmul wceq crn wral
      copab2 df-hl cncv df-phl ineq2i wss bnncv ssriv df-ss mpbi ineq1i eqcomi
      inass eqtr ) FGHIZGAJZBJZCJZKDJZLMNKUQOPUREJKUSKUTLMNKQKMUQUTLMNKURUTLMNK
      QKRKSBUSTZUAAVAUACEDUBZIZUCUPGUDVBIZIZVCHVDGABCDEUEUFVCVEVCGUDIZVBIZVEVGV
      CVFGVBGUDUGVFGSAGUDUQUHUIGUDUJUKULUMGUDVBUNUOUMUOUO $.
      $( [2-Apr-2007] $) $( [2-Apr-2007] $)

    $( Every complex Hilbert space is a complex Banach space. $)
    hlbnOLD $p |- ( U e. CHil -> U e. CBan ) $=
      ( vx vy vg vn vs chl cbn cv co cfv c2 cexp c1 cneg caddc cmul wceq crn
      wral copab2 cin dfhlOLD inss1 eqsstr sseli ) GHAGHBIZCIZDIZJEIZKLMJUGNOUH
      FIJUIJUJKLMJPJLUGUJKLMJUHUJKLMJPJQJRCUISZTBUKTDFEUAZUBHBCDEFUCHULUDUEUF
      $.
      $( [2-Apr-2007] $) $( [17-Mar-2007] $)

    $( Every complex Hilbert space is a complex Banach space.  (Contributed by
        Steve Rodriguez, 29-Apr-2007.) $)
    hlbn $p |- ( U e. CHil -> U e. CBan ) $=
      ( chl cbn cphl cin df-hl inss1 eqsstr sseli ) BCABCDECFCDGHI $.
      $( [29-Apr-2007] $) $( [28-Apr-2007] $)

    $( The class of all complex Hilbert spaces is a relation. $)
    hlrel $p |- Rel CHil $=
      ( vx chl cbn wss wrel cv hlbnOLD ssriv bnrel relss mp2 ) BCDCEBEABCAFGHIBCJK
      $.
      $( [2-Apr-2007] $) $( [17-Mar-2007] $)
  $}

  $( Every complex Hilbert space is a normed complex vector space. $)
  hlncv $p |- ( U e. CHil -> U e. NrmCVec ) $=
    ( chl wcel cbn cncv hlbnOLD bnncv syl ) ABCADCAECAFAGH $.
    $( [7-Apr-2007] $) $( [17-Mar-2007] $)

  $( The predicate "is a complex Hilbert space."  A Hilbert space is a
     Banach space whose norm satisfies the parallelogram law.  (Contributed by
     Steve Rodriguez, 29-Apr-2007.) $)
  ishlg $p |- ( U e. CHil <-> ( U e. CBan /\ U e. CPreHil ) ) $=
   ( chl wcel cbn cphl cin wa df-hl eleq2i elin bitr ) ABCADEFZCADCAECGBLAHI
   ADEJK $.
   $( [29-Apr-2007] $) $( [28-Apr-2007] $)

  ${
    $d g n s x y G $.  $d g n s x y N $.  $d g n s x y S $.  $d g n s x y X $.
    $d g n s Z $.
    ishlg.1 $e |- X = ran G $.
    $( The predicate "is a complex Hilbert space."  A Hilbert space is a
       Banach space whose norm satisfies the parallelogram law.  The vector
       (group) addition operation is ` G ` , the scalar product is ` S ` , and
       the norm is ` N ` . $)
    ishlgOLD $p |- ( ( G e. A /\ S e. B /\ N e. C ) ->
  ( <. <. G , S >. , N >. e. CHil <-> ( <. <. G , S >. , N >. e. CBan /\
          A. x e. X A. y e. X
      ( ( ( N ` ( x G y ) ) ^ 2 ) + ( ( N ` ( x G ( -u 1 S y ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( N ` x ) ^ 2 ) + ( ( N ` y ) ^ 2 ) ) ) ) ) ) $=
      ( vg vn vs wcel w3a cop cbn cv co cfv c2 cexp c1 cneg caddc cmul wceq
      crn wral copab2 wa chl rneq syl6eqr raleq1d opreq fveq2d opreq1d
      opreq12d eqeq1d ralbidv bitrd opreq2d 2ralbidv fveq1 eqeq12d eloprabg
      anbi2d cin dfhlOLD eleq2i elin bitr syl5bb ) GCNFDNHENOZGFPHPZQNZVPARZBRZ
      KRZSZLRZTZUAUBSZVRUCUDZVSMRZSZVTSZWBTZUAUBSZUESZUAVRWBTZUAUBSZVSWBTZUAUBS
      ZUESZUFSZUGZBVTUHZUIZAWSUIZKMLUJZNZUKZVQVRVSGSZHTZUAUBSZVRWEVSFSZGSZHTZUA
      UBSZUESZUAVRHTZUAUBSZVSHTZUAUBSZUESZUFSZUGZBIUIZAIUIZUKVPULNZVOXCYAVQXAXE
      WBTZUAUBSZVRWGGSZWBTZUAUBSZUESZWQUGZBIUIZAIUIZYDXIWBTZUAUBSZUESZWQUGZBIUI
      ZAIUIYAKMLGFHCDEVTGUGZXAWTAIUIYKYQWTAWSIYQWSGUHIVTGUMJUNZUOYQWTYJAIYQWTWR
      BIUIYJYQWRBWSIYRUOYQWRYIBIYQWKYHWQYQWDYDWJYGUEYQWCYCUAUBYQWAXEWBVRVSVTGUP
      UQURYQWIYFUAUBYQWHYEWBVRWGVTGUPUQURUSUTVAVBVAVBWFFUGZYIYOABIIYSYHYNWQYSYG
      YMYDUEYSYFYLUAUBYSYEXIWBYSWGXHVRGWEVSWFFUPVCUQURVCUTVDWBHUGZYPXTAIYTYOXSB
      IYTYNXLWQXRYTYDXGYMXKUEYTYCXFUAUBXEWBHVEURYTYLXJUAUBXIWBHVEURUSYTWPXQUAUF
      YTWMXNWOXPUEYTWLXMUAUBVRWBHVEURYTWNXOUAUBVSWBHVEURUSVCVFVAVAVGVHYBVPQXBVI
      ZNXDULUUAVPABKLMVJVKVPQXBVLVMVN $.
      $( [17-Mar-2007] $) $( [17-Jan-2007] $)
  $}

  ${
    hlop.1 $e |- W = ( 1st ` U ) $.
    hlop.2 $e |- G = ( 1st ` W ) $.
    hlop.3 $e |- S = ( 2nd ` W ) $.
    hlop.4 $e |- N = ( 2nd ` U ) $.
    $( A complex Hilbert space in terms of ordered pair components. $)
    hlop $p |- ( U e. CHil -> U = <. <. G , S >. , N >. ) $=
      ( chl wcel c1st cfv c2nd cop wrel wceq hlrel 1st2nd mpan cncv cvc hlncv
      ncvv vcrel opeq12i syl6eqr 3syl opeq1d syl5eqr eqtrd ) BJKZBBLMZBNMZOZCAO
      ZDOZJPULBUOQRBJSTULEDOUQUOULEUPDULBUAKEUBKZEUPQBUCBEFUDUREELMZENMZOZUPUBP
      UREVAQUEEUBSTCUSAUTGHUFUGUHUIEUMDUNFIUFUJUK $.
      $( [9-Apr-2007] $) $( [17-Mar-2007] $)
  $}

  ${
    hlpar.1 $e |- W = ( 1st ` U ) $.
    hlpar.2 $e |- G = ( 1st ` W ) $.
    hlpar.3 $e |- S = ( 2nd ` W ) $.
    hlpar.4 $e |- N = ( 2nd ` U ) $.
    hlpar.5 $e |- X = ran G $.
    $d x y A $.  $d y B $.  $d x y G $.  $d x y N $.  $d x y S $.  $d x y X $.
    $( The parallelogram law satified by Hilbert space vectors.  (Contributed
       by Steve Rodriguez, 29-Apr-2007.) $)
    hlpar $p |- ( ( U e. CHil /\ A e. X /\ B e. X ) ->
      ( ( ( N ` ( A G B ) ) ^ 2 ) + ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( N ` A ) ^ 2 ) + ( ( N ` B ) ^ 2 ) ) ) ) $=
      ( vx vy chl wcel w3a cv co cfv c2 cexp c1 cneg caddc cmul wceq wral cvv
      cop cphl wa cncv isphlg biimpa pm3.27d cbn ishlg pm3.27bd sylan2 c1st
      fvex eqeltr c2nd 3pm3.2i a1i hlop eleq1d ibi sylanc 3ad2ant1 wi opreq1
      fveq2d opreq1d opreq12d fveq2 opreq2d eqeq12d opreq2 rcla42v 3adant1 mpd
      ) DPQZAHQZBHQZRNSZOSZETZFUAZUBUCTZWHUDUEZWICTZETZFUAZUBUCTZUFTZUBWHFUAZUB
      UCTZWIFUAZUBUCTZUFTZUGTZUHZOHUINHUIZABETZFUAZUBUCTZAWMBCTZETZFUAZUBUCTZUF
      TZUBAFUAZUBUCTZBFUAZUBUCTZUFTZUGTZUHZWEWFXFWGEUJQZCUJQZFUJQZRZECUKFUKZPQZ
      XFWEYEYFULQZXFYGYEYHUMYFUNQZXFYEYHYIXFUMNOUJUJUJCEFHMUOUPUQYGYFURQYHYFUSU
      TVAYEWEYBYCYDEGVBUAUJJGVBVCVDCGVEUAUJKGVEVCVDFDVEUAUJLDVEVCVDVFVGWEYGWEDY
      FPCDEFGIJKLVHVIVJVKVLWFWGXFYAVMWEXEYAAWIETZFUAZUBUCTZAWNETZFUAZUBUCTZUFTZ
      UBXPXBUFTZUGTZUHNOABHHWHAUHZWRYPXDYRYSWLYLWQYOUFYSWKYKUBUCYSWJYJFWHAWIEVN
      VOVPYSWPYNUBUCYSWOYMFWHAWNEVNVOVPVQYSXCYQUBUGYSWTXPXBUFYSWSXOUBUCWHAFVRVP
      VPVSVTWIBUHZYPXNYRXTYTYLXIYOXMUFYTYKXHUBUCYTYJXGFWIBAEWAVOVPYTYNXLUBUCYTY
      MXKFYTWNXJAEWIBWMCWAVSVOVPVQYTYQXSUBUGYTXBXRXPUFYTXAXQUBUCWIBFVRVPVSVSVTW
      BWCWD $.
      $( [29-Apr-2007] $) $( [28-Apr-2007] $)

    $( The parallelogram law satified by Hilbert space vectors. $)
    hlparOLD $p |- ( ( U e. CHil /\ A e. X /\ B e. X ) ->
      ( ( ( N ` ( A G B ) ) ^ 2 ) + ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( N ` A ) ^ 2 ) + ( ( N ` B ) ^ 2 ) ) ) ) $=
      ( vx vy chl wcel w3a cv co cfv c2 cexp c1 cneg caddc cmul wceq wral cvv
      cop wa cbn ishlgOLD biimpa pm3.27d c1st fvex eqeltr c2nd 3pm3.2i a1i hlop
      eleq1d ibi sylanc 3ad2ant1 wi opreq1 fveq2d opreq1d opreq12d fveq2
      opreq2d eqeq12d opreq2 rcla42v 3adant1 mpd ) DPQZAHQZBHQZRNSZOSZETZFUAZUB
      UCTZWCUDUEZWDCTZETZFUAZUBUCTZUFTZUBWCFUAZUBUCTZWDFUAZUBUCTZUFTZUGTZUHZOHU
      INHUIZABETZFUAZUBUCTZAWHBCTZETZFUAZUBUCTZUFTZUBAFUAZUBUCTZBFUAZUBUCTZUFTZ
      UGTZUHZVTWAXAWBEUJQZCUJQZFUJQZRZECUKFUKZPQZXAVTXTYBULYAUMQZXAXTYBYCXAULNO
      UJUJUJCEFHMUNUOUPXTVTXQXRXSEGUQUAUJJGUQURUSCGUTUAUJKGUTURUSFDUTUAUJLDUTUR
      USVAVBVTYBVTDYAPCDEFGIJKLVCVDVEVFVGWAWBXAXPVHVTWTXPAWDETZFUAZUBUCTZAWIETZ
      FUAZUBUCTZUFTZUBXKWQUFTZUGTZUHNOABHHWCAUHZWMYJWSYLYMWGYFWLYIUFYMWFYEUBUCY
      MWEYDFWCAWDEVIVJVKYMWKYHUBUCYMWJYGFWCAWIEVIVJVKVLYMWRYKUBUGYMWOXKWQUFYMWN
      XJUBUCWCAFVMVKVKVNVOWDBUHZYJXIYLXOYNYFXDYIXHUFYNYEXCUBUCYNYDXBFWDBAEVPVJV
      KYNYHXGUBUCYNYGXFFYNWIXEAEWDBWHCVPVNVJVKVLYNYKXNUBUGYNWQXMXKUFYNWPXLUBUCW
      DBFVMVKVNVNVOVQVRVS $.
      $( [10-Apr-2007] $) $( [17-Mar-2007] $)
  $}

