$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CHil $.

  $( Extend class notation with the class of all complex Hilbert spaces. $)
  chl $a class CHil $.

  $( Define the class of all complex Hilbert spaces.  A Hilbert space is a
     Banach space which is also an inner product space. $)
  df-hl $a |- CHil = ( CBan i^i CPreHil ) $.

  $( The predicate "is a complex Hilbert space."  A Hilbert space is a
     Banach space which is also an inner product space, i.e. whose norm
     satisfies the parallelogram law.  (Contributed by Steve Rodriguez,
     28-Apr-2007.) $)
  ishl $p |- ( U e. CHil <-> ( U e. CBan /\ U e. CPreHil ) ) $=
    ( chl wcel cbn cphl cin wa df-hl eleq2i elin bitr ) ABCADEFZCADCAECGBLAHIAD
    EJK $.
    $( [8-Sep-2007] $) $( [28-Apr-2007] $)

  $( Every complex Hilbert space is a complex Banach space.  (Contributed by
      Steve Rodriguez, 28-Apr-2007.) $)
  hlbn $p |- ( U e. CHil -> U e. CBan ) $=
    ( chl wcel cbn cphl ishl pm3.26bi ) ABCADCAECAFG $.
    $( [8-Sep-2007] $) $( [28-Apr-2007] $)

  $( Every complex Hilbert space is an inner product space (also called a
     pre-Hilbert space). $)
  hlph $p |- ( U e. CHil -> U e. CPreHil ) $=
    ( chl wcel cbn cphl ishl pm3.27bi ) ABCADCAECAFG $.
    $( [17-Dec-2007] $) $( [28-Apr-2007] $)

  $( The class of all complex Hilbert spaces is a relation. $)
  hlrel $p |- Rel CHil $=
    ( vx chl cbn wss wrel cv hlbn ssriv bnrel relss mp2 ) BCDCEBEABCAFGHIBCJK
    $.
    $( [?] $) $( [17-Mar-2007] $)

  $( Every complex Hilbert space is a normed complex vector space. $)
  hlnv $p |- ( U e. CHil -> U e. NrmCVec ) $=
    ( chl wcel cbn cnv hlbn bnnv syl ) ABCADCAECAFAGH $.
    $( [9-Sep-2007] $) $( [17-Mar-2007] $)

  ${
    hlvc.1 $e |- W = ( 1st ` U ) $.
    $( Every complex Hilbert space is a complex vector space. $)
    hlvc $p |- ( U e. CHil -> W e. CVec ) $=
      ( chl wcel cnv cvc hlnv nvvc syl ) ADEAFEBGEAHABCIJ $.
      $( [9-Sep-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    hlcms.8 $e |- D = ( IndMet ` U ) $.
    $( The induced metric on a complex Hilbert space is complete. $)
    hlcms $p |- ( U e. CHil -> D e. CMet ) $=
      ( chl wcel cbn cms hlbn bncms syl ) BDEBFEAGEBHABCIJ $.
      $( [16-Dec-2007] $) $( [8-Sep-2007] $)

    $( The induced metric on a complex Hilbert space. $)
    hlmet $p |- ( U e. CHil -> D e. Met ) $=
      ( chl wcel cnv cme hlnv imsmet syl ) BDEBFEAGEBHABCIJ $.
      $( [?] $) $( [7-Sep-2007] $)
  $}

  ${
    hlpar.1 $e |- X = ( Base ` U ) $.
    hlpar.2 $e |- G = ( +v ` U ) $.
    hlpar.3 $e |- M = ( -v ` U ) $.
    hlpar.4 $e |- S = ( .s ` U ) $.
    hlpar.6 $e |- N = ( norm ` U ) $.
    $( The parallelogram law satified by Hilbert space vectors.  (Contributed
       by Steve Rodriguez, 28-Apr-2007.) $)
    hlpar $p |- ( ( U e. CHil /\ A e. X /\ B e. X ) ->
      ( ( ( N ` ( A G B ) ) ^ 2 ) + ( ( N ` ( A M B ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( N ` A ) ^ 2 ) + ( ( N ` B ) ^ 2 ) ) ) ) $=
      ( cphl wcel co cfv c2 cexp caddc cmul wceq chl phpar hlph syl3an1 ) DNOAH
      OBHOABEPGQRSPABFPGQRSPTPRAGQRSPBGQRSPTPUAPUBDUCOABDEFGHIJKMUDDUEUF $.
      $( [?] $) $( [28-Apr-2007] $)
  $}

  ${
    hlparOLD.1 $e |- X = ( Base ` U ) $.
    hlparOLD.2 $e |- G = ( +v ` U ) $.
    hlparOLD.4 $e |- S = ( .s ` U ) $.
    hlparOLD.6 $e |- N = ( norm ` U ) $.
    $( The parallelogram law satified by Hilbert space vectors.  (Contributed
       by Steve Rodriguez, 28-Apr-2007.) $)
    hlparOLD $p |- ( ( U e. CHil /\ A e. X /\ B e. X ) ->
      ( ( ( N ` ( A G B ) ) ^ 2 ) + ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( N ` A ) ^ 2 ) + ( ( N ` B ) ^ 2 ) ) ) ) $=
      ( cphl wcel co cfv c2 cexp c1 cneg caddc cmul wceq chl phparOLD hlph
      syl3an1 ) DLMAGMBGMABENFOPQNARSBCNENFOPQNTNPAFOPQNBFOPQNTNUANUBDUCMABCDEF
      GHIJKUDDUEUF $.
      $( [?] $) $( [28-Apr-2007] $)
  $}

