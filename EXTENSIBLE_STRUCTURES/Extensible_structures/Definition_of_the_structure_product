$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Definition of the structure product
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c topGen $.
  $c tP $.

  $( Extend class notation with a function that converts a basis to its
     corresponding topology. $)
  ctg $a class topGen $.

  $( Extend class notation with a function whose value is a product
     topology. $)
  cpt $a class tP $.

  ${
    $d f u x y z $.
    $( Define a function that converts a basis to its corresponding topology.
       Equivalent to the definition of a topology generated by a basis in
       [Munkres] p. 78 (see ~ tgval2 ).  See ~ tgval3 for an alternate
       expression for the value. $)
    df-topgen $a |- topGen = ( x e. _V |->
                               { y | y C_ U. ( x i^i ~P y ) } ) $.

    $( Define the product topology on a collection of topologies.  For
       convenience, it is defined on arbitrary collections of sets, expressed
       as a function from some index set to the subbases of each factor
       space. $)
    df-pt $a |- tP = ( f e. { f | Fun f } |-> ( topGen ` ( fi ` { x |
                       E. y e. dom f E. u e. ( f ` y ) x = X_ z e. dom f
                         if ( z = y , u , U. ( f ` z ) ) } ) ) ) $.
  $}

  $c Xm_ Xs_ ^s $.

  $( The function constructing module products. $)
  cprdm $a class Xm_ $.

  $( The function constructing structure products. $)
  cprs $a class Xs_ $.

  $( The function constructing structure powers. $)
  cpws $a class ^s $.

  ${
    $d s r x v f g $.
    $( Define a module product, which is a structure product with an externally
       specified ring of scalars.  This can be a product of groups, rings,
       modules, or ordered topological fields; any unused components will have
       garbage in them but this usually not relevant for the purpos of
       inheriting the structures present in the factors. $)
    df-prdm $a |- Xm_ = ( s e. _V , r e. _V |->
      [_ X_ x e. dom r ( Base ` ( r ` x ) ) / v ]_
      ( ( { <. ( Base ` ndx ) , v >. ,
        <. ( +g ` ndx ) , ( f e. v , g e. v |-> ( x e. dom r |->
          ( ( f ` x ) ( +g ` ( r ` x ) ) ( g ` x ) ) ) ) >. ,
        <. ( .r ` ndx ) , ( f e. v , g e. v |-> ( x e. dom r |->
          ( ( f ` x ) ( .r ` ( r ` x ) ) ( g ` x ) ) ) ) >. } u.
      { <. ( Scalar ` ndx ) , s >. ,
        <. ( vsca ` ndx ) , ( f e. ( Base ` s ) , g e. v |->
        ( x e. dom r |-> ( f ( vsca ` ( r ` x ) ) ( g ` x ) ) ) ) >. } ) u.
      { <. ( TopOpen ` ndx ) , ( tP ` ( TopOpen o. r ) ) >. ,
        <. ( le ` ndx ) , { <. f , g >. | ( { f , g } C_ v /\
        A. x e. dom r ( f ` x ) ( le ` ( r ` x ) ) ( g ` x ) ) } >. ,
        <. ( dist ` ndx ) , ( f e. v , g e. v |-> sup ( ran ( x e. dom r |->
       ( ( f ` x ) ( dist ` ( r ` x ) ) ( g ` x ) ) ) , RR* , < ) ) >. } ) ) $.

    $( Define a structure product.  This is defined in terms of the module
       product, but the scalar ring is ignored so we can set it to anything
       (here we use the empty set). $)
    df-prd $a |- Xs_ = ( r e. _V |-> ( (/) Xm_ r ) ) $.

    $d r i $.
    $( Define a structure power, which is just a structure product where all
       the factors are the same. $)
    df-pws $a |- ^s = ( r e. _V , i e. _V |->
      ( ( Scalar ` r ) Xm_ ( i X. { r } ) ) ) $.
  $}

  $( Structure definition utility lemma.  To prove that an explicit function is
     a function using O(n) steps, exploit the order properties of the index
     set.  Zero-pair case.  (Contributed by Stefan O'Rear, 3-Jan-2015.) $)
  strlemor0 $p |- ( Fun (/) /\ dom (/) C_ ( 1 ... 0 ) ) $=
    ( c0 wfun cdm c1 cc0 cfz co wss fun0 dm0 0ss eqsstri pm3.2i ) ABACZDEFGZHIN
    AOJOKLM $.
    $( [3-Jan-2015] $)

  ${
    strlemor.o $e |- I < J $.
    strlemor.i $e |- I e. NN0 $.
    strlemor.j $e |- J e. NN $.
    strlemor.f $e |- ( Fun F /\ dom F C_ ( 1 ... I ) ) $.
    ${
      strlemor1.g $e |- G = ( F u. { <. J , X >. } ) $.
      $( Add one element to the end of a structure.  (Contributed by Stefan
         O'Rear, 3-Jan-2015.) $)
      strlemor1 $p |- ( Fun G /\ dom G C_ ( 1 ... J ) ) $=
        ( wfun cdm c1 cfz co wss csn c0 wcel wbr cop cun wa cin simpli cn elexi
        wceq funsn pm3.2i dmsnop ineq2i wn cle nn0rei nnrei ltnlei mpbi elfzle2
        clt mto simpri sseli disjsn mpbir eqtri funun mp2an funeqi dmun cuz cfv
        dmeqi nn0zi nnzi ltleii eluz2 mpbir3an fzss2 ax-mp sstri elfz1end snssi
        cz eqsstri unssi ) BKZBLZMDNOZPWGADEUAQZUBZKZAKZWJKZUCALZWJLZUDZRUHWLWM
        WNWMWOMCNOZPZIUEDEDUFHUGUIUJWQWODQZUDZRWPWTWODEUKZULXARUHDWOSZUMXCDWRSZ
        XDDCUNTZCDUTTXEUMFCDCGUOZDHUPZUQURDMCUSVAWOWRDWMWSIVBZVCVAWODVDVEVFAWJV
        GVHBWKJVIVEWHWOWPUBZWIWHWKLXIBWKJVMAWJVJVFWOWPWIWOWRWIXHDCVKVLSZWRWIPXJ
        CWDSDWDSCDUNTCGVNDHVOCDXFXGFVPCDVQVRCMDVSVTWAWPWTWIXBDWISZWTWIPDUFSXKHD
        WBURDWIWCVTWEWFWEUJ $.
        $( [3-Jan-2015] $)
    $}

    strlemor2.o $e |- J < K $.
    strlemor2.k $e |- K e. NN $.
    ${
      strlemor2.g $e |- G = ( F u. { <. J , X >. , <. K , Y >. } ) $.
      $( Add one element to the end of a structure.  (Contributed by Stefan
         O'Rear, 3-Jan-2015.) $)
      strlemor2 $p |- ( Fun G /\ dom G C_ ( 1 ... K ) ) $=
        ( cop csn cun nnnn0i eqid strlemor1 cpr df-pr uneq2i unass 3eqtr4i ) AD
        FOZPZQZBDEGLDJRMAUHCDFHIJKUHSTAUFEGOZUAZQAUGUIPZQZQBUHUKQUJULAUFUIUBUCN
        AUGUKUDUET $.
        $( [3-Jan-2015] $)
    $}

    strlemor3.o $e |- K < L $.
    strlemor3.l $e |- L e. NN $.
    ${
      strlemor3.g $e |- G = ( F u. { <. J , X >. , <. K , Y >. ,
          <. L , Z >. } ) $.
      $( Add three elements to the end of a structure.  (Contributed by Stefan
         O'Rear, 3-Jan-2015.) $)
      strlemor3 $p |- ( Fun G /\ dom G C_ ( 1 ... L ) ) $=
        ( cop cun cpr nnnn0i strlemor2 ctp df-tp uneq2i unass 3eqtr4i strlemor1
        eqid csn ) ADGSZEHSZUAZTZBEFIPEOUBQAUOCDEGHJKLMNOUOUJUCAULUMFISZUDZTAUN
        UPUKZTZTBUOURTUQUSAULUMUPUEUFRAUNURUGUHUI $.
        $( [3-Jan-2015] $)
    $}
  $}

  ${
    $d S s r x v f g $.  $d R s r x v f g $.  $d W s r x v f g $.
    $d Z s r x v f g $.  $d B s r v f g $.  $d A s r v $.  $d T s r v $.
    $d M s r v $.  $d O s r x v f g $.  $d L s r v $.  $d D s r v $.
    $d U s r v $.
    $( The structure product is a well-behaved binary operator.  (Contributed
       by Stefan O'Rear, 7-Jan-2015.) $)
    reldmprdm $p |- Rel dom Xm_ $=
      ( vs vr vv vx vf vg cvv cv cfv cbs cnx cop cplusg co cmpt cmpt2 cmulr ctp
      cvsca cpr cdm cixp csca cun cts ccom cpt cple wss wbr wral wa cds crn cxr
      copab clt csup csb cprdm df-prdm reldmmpt2 ) ABGGCDBHZUAZDHZVCIZJIUBKJICH
      ZLKMIEFVGVGDVDVEEHZIZVEFHZIZVFMINOPLKQIEFVGVGDVDVIVKVFQINOPLRKUCIAHZLKSIE
      FVLJIVGDVDVHVKVFSINOPLTUDKUEIUEVCUFUGILKUHIVHVJTVGUIVIVKVFUHIUJDVDUKULEFU
      PLKUMIEFVGVGDVDVIVKVFUMINOUNUOUQURPLRUDUSUTDCEFABVAVB $.
      $( [7-Jan-2015] $)

    prdmval.b $e |- B = X_ x e. dom R ( Base ` ( R ` x ) ) $.
    $( Lemma for structure products. $)
    prdmbasex $p |- ( R e. W -> B e. _V ) $=
      ( wcel cdm cv cfv cbs cixp cvv wral dmexg fvex rgenw a1i syl2anc syl5eqel
      ixpexg ) CDFZBACGZAHCIZJIZKZLEUAUBLFUDLFZAUBMZUELFCDNUGUAUFAUBUCJOPQAUBUD
      LLTRS $.
      $( [3-Jan-2015] $)

    prdmval.a $e |- A = ( f e. B , g e. B |-> ( x e. dom R |->
        ( ( f ` x ) ( +g ` ( R ` x ) ) ( g ` x ) ) ) ) $.
    prdmval.t $e |- T = ( f e. B , g e. B |-> ( x e. dom R |->
        ( ( f ` x ) ( .r ` ( R ` x ) ) ( g ` x ) ) ) ) $.
    prdmval.m $e |- M = ( f e. ( Base ` S ) , g e. B |-> ( x e. dom R |->
        ( f ( vsca ` ( R ` x ) ) ( g ` x ) ) ) ) $.
    prdmval.o $e |- O = ( tP ` ( TopOpen o. R ) ) $.
    prdmval.l $e |- L = { <. f , g >. | ( { f , g } C_ B /\
        A. x e. dom R ( f ` x ) ( le ` ( R ` x ) ) ( g ` x ) ) } $.
    prdmval.d $e |- D = ( f e. B , g e. B |-> sup ( ran ( x e. dom R |->
        ( ( f ` x ) ( dist ` ( R ` x ) ) ( g ` x ) ) ) , RR* , < ) ) $.
    prdmval.u $e |- U = ( ( { <. ( Base ` ndx ) , B >. ,
        <. ( +g ` ndx ) , A >. , <. ( .r ` ndx ) , T >. } u.
        { <. ( Scalar ` ndx ) , S >. , <. ( vsca ` ndx ) , M >. } ) u.
        { <. ( TopOpen ` ndx ) , O >. , <. ( le ` ndx ) , L >. ,
        <. ( dist ` ndx ) , D >. } ) $.
    $( Value of the structure product.  (Contributed by Stefan O'Rear,
       3-Jan-2015.) $)
    prdmval $p |- ( ( S e. W /\ R e. Z ) -> ( S Xm_ R ) = U ) $=
      ( vs vr vv wcel cvv cprdm co wceq elex cv cdm cfv cbs cixp cnx cop cplusg
      cmpt cmpt2 cmulr ctp csca cvsca cpr cun cts ccom cpt cple wss wbr wral wa
      copab cds crn cxr clt csup csb ixpeq1 fveq1 fveq2d ralrimivw ixpeq2 eqtrd
      dmeq syl syl6eqr adantl csbeq1d vex eleq1 mpbii prdmbasex mpt2eq12 anidms
      opeq2 opeq2d tpeq123d eqid mpan preq2d eqidd sseq2 anbi1d opabbidv csbied
      uneq12d oveqd mpteq12dv mpt2eq3dva adantr fveq2 mpt2eq123dv preq12d coeq2
      3ad2ant1 breqd raleqbidv anbi2d supeq1d df-prdm tpex prex eqeltri ovmpt2a
      rneqd unex syl2an ) FNUGFUHUGEUHUGZFEUIUJHUKEOUGFNULEOULUDUEFEUHUHUFAUEUM
      ZUNZAUMZYOUOZUPUOZUQZURUPUOZUFUMZUSZURUTUOZIJUUBUUBAYPYQIUMZUOZYQJUMZUOZY
      RUTUOZUJZVAZVBZUSZURVCUOZIJUUBUUBAYPUUFUUHYRVCUOZUJZVAZVBZUSZVDZURVEUOZUD
      UMZUSZURVFUOZIJUVBUPUOZUUBAYPUUEUUHYRVFUOZUJZVAZVBZUSZVGZVHZURVIUOZVIYOVJ
      ZVKUOZUSZURVLUOZUUEUUGVGZUUBVMZUUFUUHYRVLUOZVNZAYPVOZVPZIJVQZUSZURVRUOZIJ
      UUBUUBAYPUUFUUHYRVRUOZUJZVAZVSZVTWAWBZVBZUSZVDZVHZWCZHUIUVBFUKZYOEUKZVPZU
      WPUFCUWOWCZHUWSUFYTCUWOUWRYTCUKUWQUWRYTAEUNZYQEUOZUPUOZUQZCUWRYTAUXAYSUQZ
      UXDUWRYPUXAUKYTUXEUKYOEWJZAYPUXAYSWDWKUWRYSUXCUKZAUXAVOUXEUXDUKUWRUXGAUXA
      UWRYRUXBUPYQYOEWEZWFWGAUXAYSUXCWHWKWIPWLWMWNUWSUWTUUACUSZUUDIJCCUUKVBZUSZ
      UUNIJCCUUQVBZUSZVDZUVCUVDIJUVECUVHVBZUSZVGZVHZUVPUVQUVRCVMZUWBVPZIJVQZUSZ
      UWFIJCCUWKVBZUSZVDZVHZHUWSUFCUWOUYFUHUWSYNCUHUGUWRYNUWQUWRYOUHUGYNUEWOYOE
      UHWPWQWMACEUHPWRWKUUBCUKZUWOUYFUKUWSUYGUVLUXRUWNUYEUYGUUTUXNUVKUXQUYGUUCU
      XIUUMUXKUUSUXMUUBCUUAXAUYGUULUXJUUDUYGUULUXJUKIJUUBUUBCCUUKWSWTXBUYGUURUX
      LUUNUYGUURUXLUKIJUUBUUBCCUUQWSWTXBXCUYGUVJUXPUVCUYGUVIUXOUVDUVEUVEUKUYGUV
      IUXOUKUVEXDIJUVEUUBUVECUVHWSXEXBXFXLUYGUVPUVPUWEUYBUWMUYDUYGUVPXGUYGUWDUY
      AUVQUYGUWCUXTIJUYGUVSUXSUWBUUBCUVRXHXIXJXBUYGUWLUYCUWFUYGUWLUYCUKIJUUBUUB
      CCUWKWSWTXBXCXLWMXKUWSUYFUXIUUDBUSZUUNGUSZVDZUVAFUSZUVDLUSZVGZVHZUVMMUSZU
      VQKUSZUWFDUSZVDZVHZHUWSUXRUYNUYEUYRUWSUXNUYJUXQUYMUWSUXIUXIUXKUYHUXMUYIUW
      SUXIXGUWSUXJBUUDUWRUXJBUKUWQUWRUXJIJCCAUXAUUFUUHUXBUTUOZUJZVAZVBBUWRIJCCU
      UKVUBUWRUUECUGZUUKVUBUKUUGCUGZUWRAYPUUJUXAVUAUXFUWRUUIUYTUUFUUHUWRYRUXBUT
      UXHWFXMXNYAXOQWLWMXBUWSUXLGUUNUWRUXLGUKUWQUWRUXLIJCCAUXAUUFUUHUXBVCUOZUJZ
      VAZVBGUWRIJCCUUQVUGUWRVUCUUQVUGUKVUDUWRAYPUUPUXAVUFUXFUWRUUOVUEUUFUUHUWRY
      RUXBVCUXHWFXMXNYAXORWLWMXBXCUWSUVCUYKUXPUYLUWQUVCUYKUKUWRUVBFUVAXAXPUWSUX
      OLUVDUWSUXOIJFUPUOZCAUXAUUEUUHUXBVFUOZUJZVAZVBLUWSIJUVECUVHVUHCVUKUWQUVEV
      UHUKUWRUVBFUPXQXPUWSCXGUWRUVHVUKUKUWQUWRAYPUVGUXAVUJUXFUWRUVFVUIUUEUUHUWR
      YRUXBVFUXHWFXMXNWMXRSWLXBXSXLUWSUVPUYOUYBUYPUYDUYQUWSUVOMUVMUWRUVOMUKUWQU
      WRUVOVIEVJZVKUOMUWRUVNVULVKYOEVIXTWFTWLWMXBUWRUYBUYPUKUWQUWRUYAKUVQUWRUYA
      UXSUUFUUHUXBVLUOZVNZAUXAVOZVPZIJVQKUWRUXTVUPIJUWRUWBVUOUXSUWRUWAVUNAYPUXA
      UXFUWRUVTVUMUUFUUHUWRYRUXBVLUXHWFYBYCYDXJUAWLXBWMUWSUYCDUWFUWSUYCIJCCAUXA
      UUFUUHUXBVRUOZUJZVAZVSZVTWAWBZVBDUWSIJCCUWKVVAUWSVUCUWKVVAUKZVUDUWRVVBUWQ
      UWRVTUWJVUTWAUWRUWIVUSUWRAYPUWHUXAVURUXFUWRUWGVUQUUFUUHUWRYRUXBVRUXHWFXMX
      NYKYEWMYAXOUBWLXBXCXLUCWLWIWIAUFIJUDUEYFHUYSUHUCUYNUYRUYJUYMUXIUYHUYIYGUY
      KUYLYHYLUYOUYPUYQYGYLYIYJYM $.
      $( [3-Jan-2015] $)

    $( Structure product value exists.  (Contributed by Stefan O'Rear,
       3-Jan-2015.) $)
    prdmvalex $p |- U e. _V $=
      ( cnx cbs cfv cop cmulr ctp csca cvsca cpr cun cts cple cds cvv tpex prex
      cplusg unex eqeltri ) HUBUCUDCUEZUBURUDBUEZUBUFUDGUEZUGZUBUHUDFUEZUBUIUDL
      UEZUJZUKZUBULUDMUEZUBUMUDKUEZUBUNUDDUEZUGZUKUOUAVHVLVDVGVAVBVCUPVEVFUQUSV
      IVJVKUPUSUT $.
      $( [3-Jan-2015] $)

    $( Structure product value is a function.  (Contributed by Stefan O'Rear,
       3-Jan-2015.) $)
    prdmvalfun $p |- Fun U $=
      ( wfun cdm c1 c10 c2 caddc cfz wss cnx cbs cfv cop cplusg cmulr ctp cvsca
      co csca cpr cun c6 c9 c8 clt wbr 6lt8 8lt9 6re 8re 9re lttri mp2an nnnn0i
      6nn 9nn c3 c5 3lt5 3nn0 5nn cc0 lt01 0nn0 1nn strlemor0 1lt2 2nn 2lt3 3nn
      c0 un0 wceq wtru basendx opeq1i a1i df-plusg ndxarg df-mulr tpeq123d trud
      uncom 3eqtr4ri strlemor3 5lt6 df-sca df-vsca uneq2i strlemor2 ltp1i df-10
      preq12i breqtrri 10nn cr wcel crp 10re 2re 2pos elrpii ltaddrp cn nnaddcl
      cts cple cds df-topset df-ple df-ds eqtri simpli ) HUBHUCUDUEUFUGURZUHURU
      IUJUKULZCUMZUJUNULZBUMZUJUOULZGUMZUPZUJUSULZFUMZUJUQULZLUMZUTZVAZHVBVCUEY
      NMKDVBVDVEVFVDVCVEVFVBVCVEVFVGVHVBVDVCVIVJVKVLVMVBVOVNVPUUAUUGVQVRVBFLVSV
      TWAWKUUAWBUDUFVQCBGWCWDWEWFWGWHWIWJUDCUMZUFBUMZVQGUMZUPZWKVAUUKWKUUKVAUUA
      UUKWLWKUUKXCUUAUUKWMWNYPUUHYRUUIYTUUJYPUUHWMWNYOUDCWOWPWQYRUUIWMWNYQUFBUN
      UFAAWRWHWSWPWQYTUUJWMWNYSVQGUOVQAAWTWJWSWPWQXAXBXDXEXFVOUUFVRFUMZVBLUMZUT
      UUAUUCUULUUEUUMUUBVRFUSVRAAXGWAWSWPUUDVBLUQVBAAXHVOWSWPXMXIXJVCVCUDUGURUE
      VEVCVKXKXLXNXOUEXPXQUFXRXQUEYNVEVFXSUFXTYAYBUEUFYCVMUEYDXQUFYDXQYNYDXQXOW
      HUEUFYEVMZHUUGUJYFULZMUMZUJYGULZKUMZUJYHULZDUMZUPZVAUUGVCMUMZUEKUMZYNDUMZ
      UPZVAUAUVAUVEUUGUVAUVEWMWNUUPUVBUURUVCUUTUVDUUPUVBWMWNUUOVCMYFVCAAYIVPWSW
      PWQUURUVCWMWNUUQUEKYGUEAAYJXOWSWPWQUUTUVDWMWNUUSYNDYHYNAAYKUUNWSWPWQXAXBX
      IYLXEYM $.
      $( [3-Jan-2015] $)

    $( Structure product base set. $)
    prdmbaslem $p |- ( ( S e. W /\ R e. Z ) -> B = ( Base ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cbs cfv cprdm wceq cvv prdmbasex prdmvalex prdmvalfun baseid
      elex cnx cop cplusg cmulr ctp csca cvsca cpr cun cts cple cds tpid1 elun1
      co opex mp2b eleqtrri strfv 3syl adantl prdmval fveq2d eqtr4d ) FNUEZEOUE
      ZUFZCHUGUHZFEUIVKZUGUHWBCWDUJZWAWBEUKUECUKUEWFEOUPACEUKPULCHUGUKUDABCDEFG
      HIJKLMPQRSTUAUBUCUMABCDEFGHIJKLMPQRSTUAUBUCUNUDUOUQUGUHZCURZWHUQUSUHBURZU
      QUTUHGURZVAZUQVBUHFURUQVCUHLURVDZVEZUQVFUHMURUQVGUHKURUQVHUHDURVAZVEZHWHW
      KUEWHWMUEWHWOUEWHWIWJWGCVLVIWHWKWLVJWHWMWNVJVMUCVNVOVPVQWCWEHUGABCDEFGHIJ
      KLMNOPQRSTUAUBUCVRVSVT $.
      $( [3-Jan-2015] $)

    $( Structure product addition. $)
    prdmplusglem $p |- ( ( S e. W /\ R e. Z ) -> A = ( +g ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cplusg cfv cprdm co cvv wceq prdmbasex cdm cv mpt2exg anidms
      cmpt syl adantl prdmvalex prdmvalfun plusgid cnx cop cbs cmulr csca cvsca
      ctp cpr cun cts cple opex tpid2 elun1 mp2b eleqtrri prdmval fveq2d eqtr4d
      cds strfv ) FNUEZEOUEZUFZBHUGUHZFEUIUJZUGUHWGBUKUEZBWHULWFWJWEWFCUKUEZWJA
      CEOPUMWKWJIJCCAEUNAUOZIUOUHWLJUOUHWLEUHUGUHUJURUKUKBQUPUQUSUTBHUGUKUDABCD
      EFGHIJKLMPQRSTUAUBUCVAABCDEFGHIJKLMPQRSTUAUBUCVBUDVCVDUGUHZBVEZVDVFUHCVEZ
      WNVDVGUHGVEZVJZVDVHUHFVEVDVIUHLVEVKZVLZVDVMUHMVEVDVNUHKVEVDWCUHDVEVJZVLZH
      WNWQUEWNWSUEWNXAUEWOWNWPWMBVOVPWNWQWRVQWNWSWTVQVRUCVSWDUSWGWIHUGABCDEFGHI
      JKLMNOPQRSTUAUBUCVTWAWB $.
      $( [3-Jan-2015] $)

    $( Structure product multiplication. $)
    prdmmulrlem $p |- ( ( S e. W /\ R e. Z ) -> T = ( .r ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cmulr cfv cprdm co cvv wceq prdmbasex adantl cv cmpt mpt2exg
      cdm anidms syl prdmvalex prdmvalfun mulrid cnx cop cbs ctp csca cvsca cpr
      cplusg cun cts cple cds opex tpid3 elun1 mp2b strfv prdmval fveq2d eqtr4d
      eleqtrri ) FNUEZEOUEZUFZGHUGUHZFEUIUJZUGUHWGGUKUEZGWHULWGCUKUEZWJWFWKWEAC
      EOPUMUNWKWJIJCCAEURAUOZIUOUHWLJUOUHWLEUHUGUHUJUPUKUKGRUQUSUTGHUGUKUDABCDE
      FGHIJKLMPQRSTUAUBUCVAABCDEFGHIJKLMPQRSTUAUBUCVBUDVCVDUGUHZGVEZVDVFUHCVEZV
      DVKUHBVEZWNVGZVDVHUHFVEVDVIUHLVEVJZVLZVDVMUHMVEVDVNUHKVEVDVOUHDVEVGZVLZHW
      NWQUEWNWSUEWNXAUEWOWPWNWMGVPVQWNWQWRVRWNWSWTVRVSUCWDVTUTWGWIHUGABCDEFGHIJ
      KLMNOPQRSTUAUBUCWAWBWC $.
      $( [3-Jan-2015] $)

    $( Structure product scalars. $)
    prdmscalem $p |- ( ( S e. W /\ R e. Z ) ->
        S = ( Scalar ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa csca cfv cprdm wceq simpl prdmvalex prdmvalfun scaid cnx cop
      co cbs cplusg cmulr ctp cvsca cpr cun cts cple cds opex prid1 elun2 elun1
      mp2b eleqtrri strfv syl prdmval fveq2d eqtr4d ) FNUEZEOUEZUFZFHUGUHZFEUIU
      QZUGUHWAVSFWBUJVSVTUKFHUGNUDABCDEFGHIJKLMPQRSTUAUBUCULABCDEFGHIJKLMPQRSTU
      AUBUCUMUDUNUOUGUHZFUPZUOURUHCUPUOUSUHBUPUOUTUHGUPVAZWEUOVBUHLUPZVCZVDZUOV
      EUHMUPUOVFUHKUPUOVGUHDUPVAZVDZHWEWHUEWEWIUEWEWKUEWEWGWDFVHVIWEWHWFVJWEWIW
      JVKVLUCVMVNVOWAWCHUGABCDEFGHIJKLMNOPQRSTUAUBUCVPVQVR $.
      $( [3-Jan-2015] $)

    $( Structure product scalar multiplication. $)
    prdmvscalem $p |- ( ( S e. W /\ R e. Z ) ->
        M = ( vsca ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cvsca cfv cprdm wceq cvv prdmbasex cbs fvex cdm cmpt mpt2exg
      co mpan prdmvalex prdmvalfun vscaid cnx cop cplusg cmulr ctp csca cpr cun
      cv cts cple cds opex prid2 elun2 elun1 mp2b eleqtrri strfv adantl prdmval
      3syl fveq2d eqtr4d ) FNUEZEOUEZUFZLHUGUHZFEUIURZUGUHWHLWJUJZWGWHCUKUEZLUK
      UEZWLACEOPULFUMUHZUKUEWMWNFUMUNIJWOCAEUOIVKAVKZJVKUHWPEUHUGUHURUPUKUKLSUQ
      USLHUGUKUDABCDEFGHIJKLMPQRSTUAUBUCUTABCDEFGHIJKLMPQRSTUAUBUCVAUDVBVCUGUHZ
      LVDZVCUMUHCVDVCVEUHBVDVCVFUHGVDVGZVCVHUHFVDZWRVIZVJZVCVLUHMVDVCVMUHKVDVCV
      NUHDVDVGZVJZHWRXAUEWRXBUEWRXDUEWTWRWQLVOVPWRXAWSVQWRXBXCVRVSUCVTWAWDWBWIW
      KHUGABCDEFGHIJKLMNOPQRSTUAUBUCWCWEWF $.
      $( [3-Jan-2015] $)

    $( Structure product topology. $)
    prdmtopsetlem $p |- ( ( S e. W /\ R e. Z ) ->
        O = ( TopOpen ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cprdm co cts cfv prdmval fveq2d cvv wceq ccom fvex prdmvalex
      cpt eqeltri prdmvalfun topsetid cnx cop cbs cplusg cmulr ctp csca cpr cun
      cvsca cple cds opex tpid1 elun2 ax-mp eleqtrri strfv syl6reqr ) FNUEEOUEU
      FZFEUGUHZUIUJHUIUJZMWAWBHUIABCDEFGHIJKLMNOPQRSTUAUBUCUKULMUMUEMWCUNMUIEUO
      ZURUJUMTWDURUPUSMHUIUMUDABCDEFGHIJKLMPQRSTUAUBUCUQABCDEFGHIJKLMPQRSTUAUBU
      CUTUDVAVBUIUJZMVCZVBVDUJCVCVBVEUJBVCVBVFUJGVCVGVBVHUJFVCVBVKUJLVCVIVJZWFV
      BVLUJKVCZVBVMUJDVCZVGZVJZHWFWJUEWFWKUEWFWHWIWEMVNVOWFWJWGVPVQUCVRVSVQVT
      $.
      $( [3-Jan-2015] $)

    $( Structure product weak ordering. $)
    prdmlelem $p |- ( ( S e. W /\ R e. Z ) -> L = ( le ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cple cfv cprdm co cvv wceq cxp wss cv cpr wbr cdm wral copab
      vex prss biimpri adantr ssopab2i df-xp 3sstr4i prdmbasex xpexg anidms syl
      ssexg sylancr adantl prdmvalex prdmvalfun pleid cnx cop cplusg cmulr csca
      cbs ctp cvsca cun cts cds tpid2 elun2 ax-mp eleqtrri strfv prdmval fveq2d
      opex eqtr4d ) FNUEZEOUEZUFZKHUGUHZFEUIUJZUGUHWTKUKUEZKXAULWSXCWRWSKCCUMZU
      NXDUKUEZXCIUOZJUOZUPCUNZAUOZXFUHXIXGUHXIEUHUGUHUQAEURUSZUFZIJUTXFCUEXGCUE
      UFZIJUTKXDXKXLIJXHXLXJXLXHXFXGCIVAJVAVBVCVDVEUAIJCCVFVGWSCUKUEZXEACEOPVHX
      MXECCUKUKVIVJVKKXDUKVLVMVNKHUGUKUDABCDEFGHIJKLMPQRSTUAUBUCVOABCDEFGHIJKLM
      PQRSTUAUBUCVPUDVQVRUGUHZKVSZVRWCUHCVSVRVTUHBVSVRWAUHGVSWDVRWBUHFVSVRWEUHL
      VSUPWFZVRWGUHMVSZXOVRWHUHDVSZWDZWFZHXOXSUEXOXTUEXQXOXRXNKWPWIXOXSXPWJWKUC
      WLWMVKWTXBHUGABCDEFGHIJKLMNOPQRSTUAUBUCWNWOWQ $.
      $( [3-Jan-2015] $)

    $( Structure product metric. $)
    prdmdistlem $p |- ( ( S e. W /\ R e. Z ) -> D = ( dist ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cds cfv cprdm cvv wceq prdmbasex adantl cdm cmpt crn cxr clt
      co csup mpt2exg anidms prdmvalex prdmvalfun dsid cnx cop cbs cplusg cmulr
      cv ctp csca cvsca cpr cun cple opex tpid3 elun2 ax-mp eleqtrri strfv 3syl
      cts prdmval fveq2d eqtr4d ) FNUEZEOUEZUFZDHUGUHZFEUIUSZUGUHWKCUJUEZDUJUEZ
      DWLUKWJWNWIACEOPULUMWNWOIJCCAEUNAVKZIVKUHWPJVKUHWPEUHUGUHUSUOUPUQURUTUJUJ
      DUBVAVBDHUGUJUDABCDEFGHIJKLMPQRSTUAUBUCVCABCDEFGHIJKLMPQRSTUAUBUCVDUDVEVF
      UGUHZDVGZVFVHUHCVGVFVIUHBVGVFVJUHGVGVLVFVMUHFVGVFVNUHLVGVOVPZVFWEUHMVGZVF
      VQUHKVGZWRVLZVPZHWRXBUEWRXCUEWTXAWRWQDVRVSWRXBWSVTWAUCWBWCWDWKWMHUGABCDEF
      GHIJKLMNOPQRSTUAUBUCWFWGWH $.
      $( [3-Jan-2015] $)
  $}

  ${
    $d P x f g $.  $d S x f g $.  $d R x f g $.  $d B x f g $.
    prdmbas.p $e |- P = ( S Xm_ R ) $.
    $( Base set of a structure product.  (Contributed by Stefan O'Rear,
       3-Jan-2015.) $)
    prdmbas $p |- ( ( S e. V /\ R e. W ) ->
        X_ x e. dom R ( Base ` ( R ` x ) ) = ( Base ` P ) ) $=
      ( vf vg wcel cvv cv cfv cbs co cmpt cmpt2 cnx cop eqid cdm cixp wceq elex
      wa cprdm cplusg cds crn cxr clt csup cmulr ctp csca cvsca cpr cun cts cpt
      ccom cple wss wbr wral copab prdmbaslem fveq2i syl6eqr syl2an ) DEJDKJZCK
      JZACUAZALZCMZNMUBZBNMZUCCFJDEUDCFUDVKVLUEVPDCUFOZNMVQAHIVPVPAVMVNHLZMZVNI
      LZMZVOUGMOPQZVPHIVPVPAVMVTWBVOUHMOPUIUJUKULQZCDHIVPVPAVMVTWBVOUMMOPQZRNMV
      PSRUGMWCSRUMMWESUNRUOMDSRUPMHIDNMVPAVMVSWBVOUPMOPQZSUQURRUSMUSCVAUTMZSRVB
      MVSWAUQVPVCVTWBVOVBMVDAVMVEUEHIVFZSRUHMWDSUNURZHIWHWFWGKKVPTWCTWETWFTWGTW
      HTWDTWITVGBVRNGVHVIVJ $.
      $( [3-Jan-2015] $)

    $( Scalar ring of a structure product.  (Contributed by Stefan O'Rear,
       5-Jan-2015.) $)
    prdmsca $p |- ( ( S e. V /\ R e. W ) -> S = ( Scalar ` P ) ) $=
      ( vx vf vg wcel cvv csca cfv co cv cmpt cmpt2 cnx cop eqid wceq cprdm cdm
      elex wa cbs cixp cplusg cds crn cxr clt csup cmulr ctp cvsca cpr cun ccom
      cts cpt cple wss wbr wral copab prdmscalem fveq2i syl6eqr syl2an ) CDJCKJ
      ZBKJZCALMZUABEJCDUDBEUDVKVLUECCBUBNZLMVMGHIGBUCZGOZBMZUFMUGZVRGVOVPHOZMZV
      PIOZMZVQUHMNPQZVRHIVRVRGVOVTWBVQUIMNPUJUKULUMQZBCHIVRVRGVOVTWBVQUNMNPQZRU
      FMVRSRUHMWCSRUNMWESUORLMCSRUPMHICUFMVRGVOVSWBVQUPMNPQZSUQURRUTMUTBUSVAMZS
      RVBMVSWAUQVRVCVTWBVQVBMVDGVOVEUEHIVFZSRUIMWDSUOURZHIWHWFWGKKVRTWCTWETWFTW
      GTWHTWDTWITVGAVNLFVHVIVJ $.
      $( [5-Jan-2015] $)

    prdmplusg.b $e |- B = ( Base ` P ) $.
    $( Addition in a structure product.  (Contributed by Stefan O'Rear,
       3-Jan-2015.) $)
    prdmplusg $p |- ( ( S e. V /\ R e. W ) ->
        ( f e. B , g e. B |-> ( x e. dom R |->
          ( ( f ` x ) ( +g ` ( R ` x ) ) ( g ` x ) ) ) ) = ( +g ` P ) ) $=
      ( wcel cvv cfv cplusg co cmpt2 cnx cop eqid cdm cv cmpt wceq elex wa cixp
      cbs cprdm cds crn cxr clt csup cmulr ctp csca cvsca cpr cun cts ccom cple
      cpt wss wbr wral copab prdmplusglem prdmbas syl6reqr mpt2eq12 syl2anc a1i
      fveq2i 3eqtr4d syl2an ) EHLEMLZDMLZFGBBADUAZAUBZFUBZNZWAGUBZNZWADNZONPUCZ
      QZCONZUDDILEHUEDIUEVRVSUFZFGAVTWFUHNUGZWKWGQZEDUIPZONZWHWIAWLWKFGWKWKAVTW
      CWEWFUJNPUCUKULUMUNQZDEFGWKWKAVTWCWEWFUONPUCQZRUHNWKSRONWLSRUONWPSUPRUQNE
      SRURNFGEUHNWKAVTWBWEWFURNPUCQZSUSUTRVANVADVBVDNZSRVCNWBWDUSWKVEWCWEWFVCNV
      FAVTVGUFFGVHZSRUJNWOSUPUTZFGWSWQWRMMWKTWLTWPTWQTWRTWSTWOTWTTVIWJBWKUDZXAW
      HWLUDWJWKCUHNBACDEMMJVJKVKZXBFGBBWKWKWGVLVMWIWNUDWJCWMOJVOVNVPVQ $.
      $( [3-Jan-2015] $)

    $( Multiplication in a structure product.  (Contributed by Mario Carneiro,
       11-Jan-2015.) $)
    prdmmulr $p |- ( ( S e. V /\ R e. W ) ->
        ( f e. B , g e. B |-> ( x e. dom R |->
          ( ( f ` x ) ( .r ` ( R ` x ) ) ( g ` x ) ) ) ) = ( .r ` P ) ) $=
      ( wcel cvv cfv cmulr co cmpt2 cnx cop eqid cdm cv cmpt wceq elex cbs cixp
      wa cprdm cplusg cds crn cxr clt csup ctp csca cvsca cpr cun cts ccom cple
      cpt wss wbr wral prdmmulrlem prdmbas syl6reqr mpt2eq12 syl2anc fveq2i a1i
      copab 3eqtr4d syl2an ) EHLEMLZDMLZFGBBADUAZAUBZFUBZNZWAGUBZNZWADNZONPUCZQ
      ZCONZUDDILEHUEDIUEVRVSUHZFGAVTWFUFNUGZWKWGQZEDUIPZONZWHWIAFGWKWKAVTWCWEWF
      UJNPUCQZWKFGWKWKAVTWCWEWFUKNPUCULUMUNUOQZDEWLRUFNWKSRUJNWOSRONWLSUPRUQNES
      RURNFGEUFNWKAVTWBWEWFURNPUCQZSUSUTRVANVADVBVDNZSRVCNWBWDUSWKVEWCWEWFVCNVF
      AVTVGUHFGVOZSRUKNWPSUPUTZFGWSWQWRMMWKTWOTWLTWQTWRTWSTWPTWTTVHWJBWKUDZXAWH
      WLUDWJWKCUFNBACDEMMJVIKVJZXBFGBBWKWKWGVKVLWIWNUDWJCWMOJVMVNVPVQ $.
      $( [11-Jan-2015] $)

    $d K f g $.
    prdmvsca.k $e |- K = ( Base ` S ) $.
    $( Scalar multiplication in a structure product.  (Contributed by Stefan
       O'Rear, 5-Jan-2015.) $)
    prdmvsca $p |- ( ( S e. V /\ R e. W ) ->
        ( f e. K , g e. B |-> ( x e. dom R |->
          ( f ( vsca ` ( R ` x ) ) ( g ` x ) ) ) ) = ( vsca ` P ) ) $=
      ( cvv cfv cvsca co cnx cop eqid wcel cdm cv cmpt cmpt2 wceq elex wa cprdm
      cbs cixp a1i prdmbas syl6reqr mpt2eq12 syl2anc cds crn cxr clt csup cmulr
      cplusg ctp csca cpr cun cts ccom cpt cple wss wbr copab prdmvscalem eqtrd
      wral fveq2i syl6eqr syl2an ) EIUAENUAZDNUAZFGHBADUBZFUCZAUCZGUCZOZWEDOZPO
      QUDZUEZCPOZUFDJUAEIUGDJUGWAWBUHZWJEDUIQZPOZWKWLWJFGEUJOZAWCWHUJOUKZWIUEZW
      NWLHWOUFZBWPUFWJWQUFWRWLMULWLWPCUJOBACDENNKUMLUNFGHBWOWPWIUOUPAFGWPWPAWCW
      EWDOZWGWHVCOQUDUEZWPFGWPWPAWCWSWGWHUQOQUDURUSUTVAUEZDEFGWPWPAWCWSWGWHVBOQ
      UDUEZRUJOWPSRVCOWTSRVBOXBSVDRVEOESRPOWQSVFVGRVHOVHDVIVJOZSRVKOWDWFVFWPVLW
      SWGWHVKOVMAWCVQUHFGVNZSRUQOXASVDVGZFGXDWQXCNNWPTWTTXBTWQTXCTXDTXATXETVOVP
      CWMPKVRVSVT $.
      $( [5-Jan-2015] $)
  $}

  ${
    $d I x y z $.  $d J y z $.  $d K y z $.
    $( Condition for an explicit member of an indexed product.  (Contributed by
       Stefan O'Rear, 4-Jan-2015.) $)
    mptelixpg $p |- ( I e. V -> ( ( x e. I |-> J ) e. X_ x e. I K <->
          A. x e. I J e. K ) ) $=
      ( vy vz wcel cvv cmpt cixp wral cv cfv wa ax-17 fvmpt2 ralimiaa wi syl wb
      elex wfn csb hbcsb1 csbeq1a cbvixp eleq2i elixp2 3anass 3bitri eqid fnmpt
      w3a simpr eqeltrd jca wf dffn2 fmpt eleq1d biimpd ralim sylbir imp impbii
      sylbi hbmpt1 hbfv hbel fveq2 eleq12d cbvral anbi2i bitri mptexg biantrurd
      wceq syl5rbb syl5bb ) BEHBIHZABCJZABDKZHZCDHZABLZUABEUBWDWBIHZWBBUCZFMZWB
      NZAWIDUDZHZFBLZOZOZWAWFWDWBFBWKKZHWGWHWMUNWOWCWPWBAFGGBDWKGMZDHFPAGWIDWQW
      IHAPZUEZAWIDUFZUGUHFBWKWBUIWGWHWMUJUKWFWNWAWOWFWHAMZWBNZDHZABLZOZWNWFXEWF
      WHXDABCWBDWBULZUMWEXCABXABHZWEOXBCDABCDWBXFQXGWEUOUPRUQWHXDWFWHBIWBURZXDW
      FSZBWBUSXHCIHZABLZXIABICWBXFUTXKXCWESZABLXIXJXLABXGXJOZXCWEXMXBCDABCIWBXF
      QVAVBRXCWEABVCTVDVGVEVFXDWMWHXCWLAFBXCFPAGGWJWKAGWIWBAGBCVHWRVIWSVJXAWIVR
      XBWJDWKXAWIWBVKWTVLVMVNVOWAWGWNABCIVPVQVSVTT $.
      $( [4-Jan-2015] $)
  $}

  ${
    $d x y z I $.  $d x y z S $.  $d x y z V $.  $d x y z W $.  $d x y z X $.
    $d x y z ph $.  $d x y z T $.  $d x y z J $.  $d x y z R $.  $d x y z Y $.
    $d x y z B $.  $d y z U $.  $d x y z K $.  $d x y z P $.  $d x y z F $.
    $d x y z G $.  $d Z x y z $.  $d N x z $.
    prdmbasmpt.y $e |- Y = ( S Xm_ R ) $.
    prdmbasmpt.b $e |- B = ( Base ` Y ) $.
    ${
      prdmbasmpt.s $e |- ( ph -> S e. V ) $.
      prdmbasmpt.i $e |- ( ph -> I e. W ) $.
      prdmbasmpt.r $e |- ( ph -> R Fn I ) $.
      $( The base set of a structure product is an indexed set product.
         (Contributed by Stefan O'Rear, 10-Jan-2015.) $)
      prdmbas2 $p |- ( ph -> X_ x e. I ( Base ` ( R ` x ) ) = B ) $=
        ( cfv cbs cixp wceq syl wcel cv cdm wfn fndm eqcomd ixpeq1 fnex syl2anc
        cvv prdmbas eqtrd syl6eqr ) ABFBUADOPOZQZIPOZCAUNBDUBZUMQZUOAFUPRUNUQRA
        UPFADFUCZUPFRNFDUDSUEBFUPUMUFSAEGTDUITZUQUORLAURFHTUSNMFHDUGUHBIDEGUIJU
        JUHUKKUL $.
        $( [10-Jan-2015] $)

      $( A constructed tuple is a point in a structure product iff each
         coordinate is in the proper base set.  (Contributed by Stefan O'Rear,
         10-Jan-2015.) $)
      prdmbasmpt $p |- ( ph -> ( ( x e. I |-> U ) e. B <->
          A. x e. I U e. ( Base ` ( R ` x ) ) ) ) $=
        ( cmpt cv cfv cbs wcel cixp wral prdmbas2 eleq2d mptelixpg syl bitr3d
        wb ) ABGFPZBGBQDRSRZUAZTZUICTFUJTBGUBZAUKCUIABCDEGHIJKLMNOUCUDAGITULUMU
        HNBGFUJIUEUFUG $.
        $( [10-Jan-2015] $)

      ${
        prdmbasmpt.t $e |- ( ph -> T e. B ) $.
        $( Points in the structure product are functions; use this with
           ~ dffn5v to establish equalities.  (Contributed by Stefan O'Rear,
           10-Jan-2015.) $)
        prdmbasfn $p |- ( ph -> T Fn I ) $=
          ( vx cv cfv cbs cixp wcel wfn prdmbas2 eleqtrrd ixpfn syl ) AEPFPQCRS
          RZTZUAEFUBAEBUHOAPBCDFGHIJKLMNUCUDPEFUGUEUF $.
          $( [10-Jan-2015] $)

        prdmbasprj.j $e |- ( ph -> J e. I ) $.
        $( Each point in a structure product restricts on each coordinate to
           the relevant base set.  (Contributed by Stefan O'Rear,
           10-Jan-2015.) $)
        prdmbasprj $p |- ( ph -> ( T ` J ) e. ( Base ` ( R ` J ) ) ) $=
          ( vx wcel cfv cv cbs wral cixp prdmbas2 eleqtrrd cvv wfn simp3bi wceq
          elixp2 syl fveq2 fveq2d eleq12d rcla4va syl2anc ) AGFSRUAZETZURCTZUBT
          ZSZRFUCZGETZGCTZUBTZSZQAERFVAUDZSZVCAEBVHPARBCDFHIJKLMNOUEUFVIEUGSEFU
          HVCRFVAEUKUIULVBVGRGFURGUJZUSVDVAVFURGEUMVJUTVEUBURGCUMUNUOUPUQ $.
          $( [10-Jan-2015] $)
      $}

      $d F x y z $.  $d G x y z $.  $d P x y z $.  $d T x y z $.
      prdmplusgval.f $e |- ( ph -> F e. B ) $.
      prdmplusgval.g $e |- ( ph -> G e. B ) $.
      ${
        prdmplusgval.p $e |- P = ( +g ` Y ) $.
        $( Value of a componentwise sum in a structure product.  (Contributed
           by Stefan O'Rear, 10-Jan-2015.) $)
        prdmplusgval $p |- ( ph -> ( F P G ) = ( x e. I |-> ( ( F ` x )
            ( +g ` ( R ` x ) ) ( G ` x ) ) ) ) $=
          ( vy vz cdm cv cfv cplusg cmpt cvv cmpt2 wcel wceq wfn fnex prdmplusg
          co syl2anc eqcomd syl5eq wa fndm syl adantr fveq1 oveqan12d mpteq12dv
          adantl mptexg ovmpt2d ) AUAUBGHCCBEUCZBUDZUAUDZUEZVJUBUDZUEZVJEUEUFUE
          ZUOZUGZBIVJGUEZVJHUEZVOUOZUGZDUHADLUFUEZUAUBCCVQUIZTAWCWBAFJUJEUHUJZW
          CWBUKOAEIULZIKUJZWDQPIKEUMUPBCLEFUAUBJUHMNUNUPUQURAVKGUKZVMHUKZUSZUSB
          VIVPIVTAVIIUKZWIAWEWJQIEUTVAVBWIVPVTUKAWGWHVLVRVNVSVOVJVKGVCVJVMHVCVD
          VFVERSAWFWAUHUJPBIVTKVGVAVH $.
          $( [10-Jan-2015] $)

        prdmplusgfval.j $e |- ( ph -> J e. I ) $.
        $( Value of a structure product sum at a single coordinate.
           (Contributed by Stefan O'Rear, 10-Jan-2015.) $)
        prdmplusgfval $p |- ( ph -> ( ( F P G ) ` J ) = ( ( F ` J )
            ( +g ` ( R ` J ) ) ( G ` J ) ) ) $=
          ( vx co cfv cv cplusg cmpt prdmplusgval fveq1d wcel wceq fveq2 fveq2d
          oveq123d eqid ovex fvmpt syl eqtrd ) AIFGCUCZUDIUBHUBUEZFUDZVAGUDZVAD
          UDZUFUDZUCZUGZUDZIFUDZIGUDZIDUDZUFUDZUCZAIUTVGAUBBCDEFGHJKLMNOPQRSTUH
          UIAIHUJVHVMUKUAUBIVFVMHVGVAIUKZVBVIVCVJVEVLVNVDVKUFVAIDULUMVAIFULVAIG
          ULUNVGUOVIVJVLUPUQURUS $.
          $( [10-Jan-2015] $)
      $}

      ${
        prdmmulrval.p $e |- T = ( .r ` Y ) $.
        $( Value of a componentwise ring product in a structure product.
           (Contributed by Mario Carneiro, 11-Jan-2015.) $)
        prdmmulrval $p |- ( ph -> ( F T G ) = ( x e. I |-> ( ( F ` x )
            ( .r ` ( R ` x ) ) ( G ` x ) ) ) ) $=
          ( vy vz cdm cv cfv cmulr co cmpt cvv cmpt2 wcel wceq wfn fnex syl2anc
          prdmmulr eqcomd syl5eq wa syl adantr fveq1 oveqan12d adantl mpteq12dv
          fndm mptexg ovmpt2d ) AUAUBGHCCBDUCZBUDZUAUDZUEZVJUBUDZUEZVJDUEUFUEZU
          GZUHZBIVJGUEZVJHUEZVOUGZUHZFUIAFLUFUEZUAUBCCVQUJZTAWCWBAEJUKDUIUKZWCW
          BULOADIUMZIKUKZWDQPIKDUNUOBCLDEUAUBJUIMNUPUOUQURAVKGULZVMHULZUSZUSBVI
          VPIVTAVIIULZWIAWEWJQIDVFUTVAWIVPVTULAWGWHVLVRVNVSVOVJVKGVBVJVMHVBVCVD
          VERSAWFWAUIUKPBIVTKVGUTVH $.
          $( [11-Jan-2015] $)

        prdmmulrfval.j $e |- ( ph -> J e. I ) $.
        $( Value of a structure product's ring product at a single coordinate.
           (Contributed by Mario Carneiro, 11-Jan-2015.) $)
        prdmmulrfval $p |- ( ph -> ( ( F T G ) ` J ) = ( ( F ` J )
            ( .r ` ( R ` J ) ) ( G ` J ) ) ) $=
          ( vx co cfv cv cmulr cmpt prdmmulrval fveq1d wcel wceq fveq2 oveq123d
          fveq2d eqid ovex fvmpt syl eqtrd ) AIFGEUCZUDIUBHUBUEZFUDZVAGUDZVACUD
          ZUFUDZUCZUGZUDZIFUDZIGUDZICUDZUFUDZUCZAIUTVGAUBBCDEFGHJKLMNOPQRSTUHUI
          AIHUJVHVMUKUAUBIVFVMHVGVAIUKZVBVIVCVJVEVLVNVDVKUFVAICULUNVAIFULVAIGUL
          UMVGUOVIVJVLUPUQURUS $.
          $( [10-Jan-2015] $)
      $}
    $}

    prdmvscaval.t $e |- T = ( vsca ` Y ) $.
    prdmvscaval.k $e |- K = ( Base ` S ) $.
    prdmvscaval.s $e |- ( ph -> S e. V ) $.
    prdmvscaval.i $e |- ( ph -> I e. W ) $.
    prdmvscaval.r $e |- ( ph -> R Fn I ) $.
    prdmvscaval.f $e |- ( ph -> F e. K ) $.
    prdmvscaval.g $e |- ( ph -> G e. B ) $.
    $( Scalar multiplication in a structure product is pointwise.  (Contributed
       by Stefan O'Rear, 10-Jan-2015.) $)
    prdmvscaval $p |- ( ph -> ( F T G ) = ( x e. I |->
        ( F ( vsca ` ( R ` x ) ) ( G ` x ) ) ) ) $=
      ( vy vz cdm cv cfv cvsca co cmpt cvv cmpt2 wcel wceq wfn syl2anc prdmvsca
      fnex eqcomd syl5eq wa fndm syl adantr id fveq1 oveqan12d adantl mpteq12dv
      mptexg ovmpt2d ) AUCUDGHJCBDUEZUCUFZBUFZUDUFZUGZVNDUGUHUGZUIZUJZBIGVNHUGZ
      VQUIZUJZFUKAFMUHUGZUCUDJCVSULZPAWDWCAEKUMDUKUMZWDWCUNRADIUOZILUMZWETSILDU
      RUPBCMDEUCUDJKUKNOQUQUPUSUTAVMGUNZVOHUNZVAZVABVLVRIWAAVLIUNZWJAWFWKTIDVBV
      CVDWJVRWAUNAWHWIVMGVPVTVQWHVEVNVOHVFVGVHVIUAUBAWGWBUKUMSBIWALVJVCVK $.
      $( [10-Jan-2015] $)

    prdmvscafval.j $e |- ( ph -> J e. I ) $.
    $( Scalar multiplication of a single coordinate in a structure product.
       (Contributed by Stefan O'Rear, 10-Jan-2015.) $)
    prdmvscafval $p |- ( ph -> ( ( F T G ) ` J ) =
        ( F ( vsca ` ( R ` J ) ) ( G ` J ) ) ) $=
      ( vx wcel cfv cvsca co cvv wceq id ovex a1i cv ax-17 fveq2 eqidd oveq123d
      fveq2d adantl prdmvscaval fvmptd syl3anc ) AAIHUEFIGUFZICUFZUGUFZUHZUIUEZ
      IFGEUHZUFVGUJAUKUCVHAFVDVFULUMAUDIFUDUNZGUFZVJCUFZUGUFZUHZVGHVIUIAUDUOVJI
      UJZVNVGUJAVOFFVKVDVMVFVOVLVEUGVJICUPUSVOFUQVJIGUPURUTAUDBCDEFGHJKLMNOPQRS
      TUAUBVAVBVC $.
      $( [10-Jan-2015] $)
  $}

  ${
    $d i r F $.  $d i r I $.  $d i r R $.
    pwsval.y $e |- Y = ( R ^s I ) $.
    pwsval.f $e |- F = ( Scalar ` R ) $.
    $( Value of a structure power.  (Contributed by Mario Carneiro,
       11-Jan-2015.) $)
    pwsval $p |- ( ( R e. V /\ I e. W ) -> Y = ( F Xm_ ( I X. { R } ) ) ) $=
      ( vr vi wcel wa cpws co csn cxp cprdm cvv wceq csca elex cfv simpl fveq2d
      cv syl6eqr sneq xpeq12 syl2anr oveq12d df-pws ovex ovmpt2a syl2an syl5eq
      id ) ADKZCEKZLFACMNZBCAOZPZQNZGUQARKCRKUSVBSURADUACEUAIJACRRIUEZTUBZJUEZV
      COZPZQNVBMVCASZVECSZLZVDBVGVAQVJVDATUBBVJVCATVHVIUCUDHUFVIVIVFUTSVGVASVHV
      IUPVCAUGVECVFUTUHUIUJJIUKBVAQULUMUNUO $.
      $( [10-Jan-2015] $)
  $}

  ${
    $d x I $.  $d x R $.  $d x V $.  $d x W $.
    pwsbas.y $e |- Y = ( R ^s I ) $.
    pwsbas.f $e |- B = ( Base ` R ) $.
    $( Base set of a structure power.  (Contributed by Mario Carneiro,
       11-Jan-2015.) $)
    pwsbas $p |- ( ( R e. V /\ I e. W ) -> ( B ^m I ) = ( Base ` Y ) ) $=
      ( vx wcel wa cbs cfv csca co cixp cmap eqid cvv wceq csn cxp cprdm pwsval
      fveq2d cv fvex a1i simpr wfn fnconstg adantr prdmbas2 fvconst2g ralrimiva
      wral ixpeq2 syl eqtr3d wi idi ixpconstg sylancl oveq1i syl6eqr 3eqtrrd )
      BDJZCEJZKZFLMBNMZCBUAUBZUCOZLMZICBLMZPZACQOZVIFVLLBVJCDEFGVJRUDUEVIICIUFZ
      VKMZLMZPZVMVOVIIVMVKVJCSEVLVLRVMRVJSJVIBNUGUHVGVHUIZVGVKCUJVHCBDUKULUMVIV
      SVNTZICUPZVTVOTVGWCVHVGWBICVGVQCJKVRBLCBVQDUNUEUOULICVSVNUQURUSVIVOVNCQOZ
      VPVIVHVNSJVOWDTVIVHUTWAVABLUGICVNESVBVCAVNCQHVDVEVF $.
      $( [10-Jan-2015] $)

    pwselbas.v $e |- V = ( Base ` Y ) $.
    pwselbas.r $e |- ( ph -> R e. W ) $.
    pwselbas.i $e |- ( ph -> I e. Z ) $.
    pwselbas.x $e |- ( ph -> X e. V ) $.
    $( An element of a structure power is a function from the index set to the
       base set of the structure.  (Contributed by Mario Carneiro,
       11-Jan-2015.) $)
    pwselbas $p |- ( ph -> X : I --> B ) $=
      ( cmap wcel cbs cfv cvv wceq pwsbas syl2anc syl6eqr eleqtrrd fvex eqeltri
      co wf wb elmapg sylancr mpbid ) AGBDPUHZQZDBGUIZAGEUNOAUNHRSZEACFQDIQZUNU
      QUAMNBCDFIHJKUBUCLUDUEABTQURUOUPUJBCRSTKCRUFUGNBDGTIUKULUM $.
      $( [11-Jan-2015] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x G $.  $d x I $.  $d x M $.  $d x R $.  $d x W $.
    $d x ph $.
    pwsplusgval.y $e |- Y = ( R ^s I ) $.
    pwsplusgval.b $e |- B = ( Base ` Y ) $.
    pwsplusgval.r $e |- ( ph -> R e. V ) $.
    pwsplusgval.i $e |- ( ph -> I e. W ) $.
    pwsplusgval.f $e |- ( ph -> F e. B ) $.
    pwsplusgval.g $e |- ( ph -> G e. B ) $.
    ${
      pwsplusgval.a $e |- A = ( +g ` R ) $.
      pwsplusgval.p $e |- P = ( +g ` Y ) $.
      $( Value of addition in a structure power. $)
      pwsplusgval $p |- ( ph -> ( F P G ) = ( F oF A G ) ) $=
        ( cfv vx csca csn cxp cprdm co cplusg cv cmpt cof cbs cvv eqid wcel a1i
        fvex wfn fnconstg syl pwsval syl2anc fveq2d syl5eq eleqtrd prdmplusgval
        wceq wa fvconst2g sylan syl6eqr oveqd mpteq2dva eqtrd cmap fveq2i eqtri
        wf cpws syl6eleq pwsbas eleqtrrd elmapg sylancr feqmptd offval2 3eqtr4d
        wb mpbid ) AFGEUBTZHEUCUDZUEUFZUGTZUFZUAHUAUHZFTZWNGTZBUFZUIZFGDUFFGBUJ
        UFAWMUAHWOWPWNWJTZUGTZUFZUIWRAUAWKUKTZWLWJWIFGHULJWKWKUMXBUMWIULUNAEUBU
        PUOOAEIUNZWJHUQNHEIURUSAFCXBPACKUKTZXBMAKWKUKAXCHJUNZKWKVFNOEWIHIJKLWIU
        MUTVAZVBVCZVDAGCXBQXGVDWLUMVEAUAHXAWQAWNHUNZVGZWTBWOWPXIWTEUGTBXIWSEUGA
        XCXHWSEVFNHEWNIVHVIVBRVJVKVLVMADWLFGADKUGTWLSAKWKUGXFVBVCVKAUAHWOWPBFGJ
        ULULOWOULUNXIWNFUPUOWPULUNXIWNGUPUOAUAHEUKTZFAFXJHVNUFZUNZHXJFVQZAFEHVR
        UFZUKTZXKAFCXOPCXDXOMKXNUKLVOVPZVSAXCXEXKXOVFNOXJEHIJXNXNUMXJUMVTVAZWAA
        XJULUNZXEXLXMWGEUKUPZOXJHFULJWBWCWHWDAUAHXJGAGXKUNZHXJGVQZAGXOXKAGCXOQX
        PVSXQWAAXRXEXTYAWGXSOXJHGULJWBWCWHWDWEWF $.
        $( [11-Jan-2015] $)
    $}

    ${
      pwsmulrval.a $e |- M = ( .r ` R ) $.
      pwsmulrval.p $e |- T = ( .r ` Y ) $.
      $( Value of multiplication in a structure power. $)
      pwsmulrval $p |- ( ph -> ( F T G ) = ( F oF M G ) ) $=
        ( cfv vx csca csn cxp cprdm co cmulr cv cmpt cof cbs cvv eqid wcel fvex
        a1i wfn fnconstg syl pwsval syl2anc fveq2d syl5eq prdmmulrval fvconst2g
        wceq eleqtrd wa sylan syl6eqr oveqd mpteq2dva eqtrd cmap wf cpws fveq2i
        eqtri syl6eleq pwsbas eleqtrrd wb sylancr mpbid feqmptd offval2 3eqtr4d
        elmapg ) AEFCUBTZGCUCUDZUEUFZUGTZUFZUAGUAUHZETZWNFTZHUFZUIZEFDUFEFHUJUF
        AWMUAGWOWPWNWJTZUGTZUFZUIWRAUAWKUKTZWJWIWLEFGULJWKWKUMXBUMWIULUNACUBUOU
        POACIUNZWJGUQNGCIURUSAEBXBPABKUKTZXBMAKWKUKAXCGJUNZKWKVFNOCWIGIJKLWIUMU
        TVAZVBVCZVGAFBXBQXGVGWLUMVDAUAGXAWQAWNGUNZVHZWTHWOWPXIWTCUGTHXIWSCUGAXC
        XHWSCVFNGCWNIVEVIVBRVJVKVLVMADWLEFADKUGTWLSAKWKUGXFVBVCVKAUAGWOWPHEFJUL
        ULOWOULUNXIWNEUOUPWPULUNXIWNFUOUPAUAGCUKTZEAEXJGVNUFZUNZGXJEVOZAECGVPUF
        ZUKTZXKAEBXOPBXDXOMKXNUKLVQVRZVSAXCXEXKXOVFNOXJCGIJXNXNUMXJUMVTVAZWAAXJ
        ULUNZXEXLXMWBCUKUOZOXJGEULJWHWCWDWEAUAGXJFAFXKUNZGXJFVOZAFXOXKAFBXOQXPV
        SXQWAAXRXEXTYAWBXSOXJGFULJWHWCWDWEWFWG $.
        $( [11-Jan-2015] $)
    $}
  $}

  ${
    $d x A $.  $d x F $.  $d x I $.  $d x K $.  $d x R $.  $d x S $.  $d x W $.
    $d x ph $.  $d x X $.
    pwsvscaval.y $e |- Y = ( R ^s I ) $.
    pwsvscaval.b $e |- B = ( Base ` Y ) $.
    pwsvscaval.s $e |- S = ( vsca ` R ) $.
    pwsvscaval.t $e |- T = ( vsca ` Y ) $.
    pwsvscaval.f $e |- F = ( Scalar ` R ) $.
    pwsvscaval.k $e |- K = ( Base ` F ) $.
    pwsvscaval.r $e |- ( ph -> R e. V ) $.
    pwsvscaval.i $e |- ( ph -> I e. W ) $.
    pwsvscaval.a $e |- ( ph -> A e. K ) $.
    pwsvscaval.x $e |- ( ph -> X e. B ) $.
    $( Scalar multiplication in a structure power is pointwise.  (Contributed
       by Mario Carneiro, 11-Jan-2015.) $)
    pwsvscafval $p |- ( ph -> ( A T X ) = ( ( I X. { A } ) oF S X ) ) $=
      ( vx co csn cxp cprdm cvsca cfv cv cmpt wcel pwsval syl2anc fveq2d syl5eq
      cof wceq oveqd cbs cvv eqid csca eqeltri a1i wfn fnconstg syl prdmvscaval
      fvex eleqtrd wa fvconst2g sylan syl6eqr mpteq2dva adantr pwselbas feqmptd
      fconstmpt offval2 eqtr4d 3eqtrd ) ABLFUEBLGHDUFUGZUHUEZUIUJZUEUDHBUDUKZLU
      JZWHWEUJZUIUJZUEZULZHBUFUGZLEURUEZAFWGBLAFMUIUJWGQAMWFUIADJUMZHKUMMWFUSTU
      ADGHJKMNRUNUOZUPUQUTAUDWFVAUJZWEGWGBLHIVBKWFWFVCWRVCWGVCSGVBUMAGDVDUJVBRD
      VDVKVEVFUAAWPWEHVGTHDJVHVIUBALCWRUCACMVAUJWROAMWFVAWQUPUQVLVJAWMUDHBWIEUE
      ZULWOAUDHWLWSAWHHUMZVMZWKEBWIXAWKDUIUJEXAWJDUIAWPWTWJDUSTHDWHJVNVOUPPVPUT
      VQAUDHBWIEWNLKIVBUAABIUMWTUBVRWIVBUMXAWHLVKVFWNUDHBULUSAUDHBWAVFAUDHDVAUJ
      ZLAXBDHCJLMKNXBVCOTUAUCVSVTWBWCWD $.
      $( [11-Jan-2015] $)

    pwsvscaval.j $e |- ( ph -> J e. I ) $.
    $( Scalar multiplication of a single coordinate in a structure power.
       (Contributed by Mario Carneiro, 11-Jan-2015.) $)
    pwsvscaval $p |- ( ph -> ( ( A T X ) ` J ) = ( A S ( X ` J ) ) ) $=
      ( co cfv csn cxp cof pwsvscafval fveq1d wcel wceq cbs wf wfn pwselbas ffn
      eqid syl wa eqidd ofc1 mpdan eqtrd ) AIBMFUFZUGIHBUHUIMEUJUFZUGZBIMUGZEUF
      ZAIVGVHABCDEFGHJKLMNOPQRSTUAUBUCUDUKULAIHUMZVIVKUNUEAHBVJEMLJIUBUCAHDUOUG
      ZMUPMHUQAVMDHCKMNLOVMUTPUAUBUDURHVMMUSVAAVLVBVJVCVDVEVF $.
      $( [11-Jan-2015] $)
  $}

