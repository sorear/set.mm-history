$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Predicate Calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x ph $.
    $( Any wff can be trivially quantified, so long as the quantifier's set is
       distinct from said wff.

       See also ~ 19.9v . $)
    quantriv $p |- ( A. x ph <-> ph ) $=
      ( ax-17 19.3 ) ABABCD $.
      $( [13-Sep-2011] $)
  $}

  $( For all sets, ` T. ` is true. $)
  allt $p |- A. x T. $=
    ( wtru tru ax-gen ) BACD $.
    $( [13-Sep-2011] $)

  $( For all sets, ` F. ` is not true. $)
  alnof $p |- A. x -. F. $=
    ( wfal wn notfal ax-gen ) BCADE $.
    $( [13-Sep-2011] $)

  $( Not all sets hold ` F. ` as true. $)
  nalf $p |- -. A. x F. $=
    ( wfal wal wn alnof FiA a4s mt2 ) BACBDACZAEBIDZAJFGH $.
    $( [13-Sep-2011] $)

  $( There exists a set that holds ` T. ` as true. $)
  extt $p |- E. x T. $=
    ( wtru wal wex allt 19.2 ax-mp ) BACBADAEBAAFG $.
    $( [13-Sep-2011] $)

  $( There does not exist a set, such that ` T. ` is not true. $)
  nextnt $p |- -. E. x -. T. $=
    ( wtru wn tru notnoti nex ) BCABDEF $.
    $( [13-Sep-2011] $)

  $( There does not exist a set, such that ` F. ` is true. $)
  nextf $p |- -. E. x F. $=
    ( wfal notfal nex ) BACD $.
    $( [13-Sep-2011] $)

  $( There does not exist exactly one set, such that ` F. ` is true. $)
  unnf $p |- -. E! x F. $=
    ( wfal weu wex nextf euex mto ) BACBADAEBAFG $.
    $( [13-Sep-2011] $)

  $( There does not exist exactly one set, such that ` T. ` is true. $)
  unnt $p |- -. E! x T. $=
    ( wtru weu wn wex nextnt eunex mto ) BACBDAEAFBAGH $.
    $( [13-Sep-2011] $)

  $( There does not exist at most one set, such that ` T. ` is true. $)
  mont $p |- -. E* x T. $=
    ( wtru wmo wex weu wi wn extt unnt mth8 mp2 df-mo mtbir ) BACBADZBAEZFZNOGP
    GAHAINOJKBALM $.
    $( [13-Sep-2011] $)

  $( There exist at most one set, such that ` F. ` is true. $)
  mof $p |- E* x F. $=
    ( wfal wex wn wmo nextf exmo ori ax-mp ) BACZDBAEZAFJKBAGHI $.
    $( [13-Sep-2011] $)

