$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Misc. Single Axiom Systems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( A single axiom for propositional calculus offered by Meredith. $)
  meran1 $p |- ( -. ( -. ( -. ph \/ ps ) \/ ( ch \/ ( th \/ ta ) ) ) \/
          ( -. ( -. th \/ ph ) \/ ( ch \/ ( ta \/ ph ) ) ) ) $=
    ( wn wo wi orc olc ja imim1i pm1.5 pm2.3 pm2.21 mth8 imim12i pm2.43d con4d
    imor 3imtr3i orim2i 3syl syl 3imtr4i pm2.24 idd jaod com12 imori ) AFZBGZFC
    DEGZGZGZDFZAGZFCEAGZGZGZULUNHZUQUSHZUOUTVAABHZUNHZDAHZUSHZVBVCULUNABULUKBIB
    UKJKLVCFZUNGZVEFZUSGZVDVFVHCVGUMGZGCVIURGZGVJVGCUMMVKVLCVKVGEDGGZVLVGDENVME
    VGDGZGEVIAGZGVLVGEDMVNVOEVCDHZVEAHVNVOVPAVEVPUKVIUKVCDUKVIHABODAPQRSVCDTVEA
    TUAUBEVIAMUCUDUBCVIURMUCVCUNTVEUSTUEUQVEUSDUQADUPAADAUFDAUGUHUILUCULUNTUQUS
    TUAUJ $.
    $( [17-Aug-2011] $) $( [13-Aug-2011] $)

  $( A single axiom for propositional calculus offered by Meredith. $)
  meran2 $p |- ( -. ( -. ( -. ph \/ ps ) \/ ( ch \/ ( th \/ ta ) ) ) \/
          ( -. ( -. ta \/ th ) \/ ( ch \/ ( ph \/ th ) ) ) ) $=
    ( wn wo meran1 imorri syl imori ) AFBGFCDEGGGZEFDGFCADGGGZLDFAGFCEAGGGZMLNA
    BCDEHINMDACEAHIJK $.
    $( [17-Aug-2011] $) $( [13-Aug-2011] $)

  $( A single axiom for propositional calculus offered by Meredith. $)
  meran3 $p |- ( -. ( -. ( -. ph \/ ps ) \/ ( ch \/ ( th \/ ta ) ) ) \/
          ( -. ( -. ch \/ ph ) \/ ( ta \/ ( th \/ ph ) ) ) ) $=
    ( wn wo wi pm2.3 imim2i pm1.5 syl6 imor 3imtr3i meran1 imorri syl imori )
    AFBGZFZCDEGGZGZCFAGFEDAGGGZUBTECDGGZGZUCSUAHZSUDHUBUEUFSCEDGGZUDUAUGSCDEIJC
    EDKLSUAMSUDMNUEUCABECDOPQR $.
    $( [17-Aug-2011] $) $( [13-Aug-2011] $)

  $( A single axiom for propositional calculus offered by Wajsberg. $)
  waj-ax $p |- ( ( ph -/\ ( ps -/\ ch ) ) -/\ ( ( ( th -/\ ch ) -/\ ( ( ph
         -/\ th ) -/\ ( ph -/\ th ) ) ) -/\ ( ph -/\ ( ph -/\ ps ) ) ) ) $=
    ( wnand wa wi nic-justlem wn pm2.27 anim2d expdimp pm3.27 imim2i syl5com
    con3d df-nand 3imtr4g nic-justim sylib pm3.21 adantr com12 a2i sylibr jca
    sylbi mpbir ) ABCEEZDCEZADEZUKEEZAABEEZEEUIULUMFZGUIABCFZGZUNACBHUPULUMUPUJ
    UKGULUPDCFZIADFZIUJUKUPURUQURACGZUQUPADUSUQAUSCDACJKLUOCABCMNOPDCQADQRUJUKS
    TUPAABFZGZUMAUOUTUOAUTBVACBAUAUBUCUDABAHUEUFUGUIUMULHUH $.
    $( [17-Aug-2011] $) $( [13-Aug-2011] $)

  $( A single axiom for propositional calculus offered by Lukasiewicz. $)
  lukshef-ax2 $p |- ( ( ph -/\ ( ps -/\ ch ) ) -/\ ( ( ph -/\ ( ch -/\ ph ) )
          -/\ ( ( th -/\ ps ) -/\ ( ( ph -/\ th ) -/\ ( ph -/\ th ) ) ) ) ) $=
    ( wnand wa wi nic-justlem biimpi ancr anim1i pm3.27 imim2i pm2.27 anim2d
    expdimp pm3.26 syl5com sylanc wn con3 df-nand 3imtr4g anim2i syl biimpri
    nic-justim anim12i 3syl mpbir ) ABCEEZACAEEZDBEZADEZUNEEZEEUKULUOFZGUKABCFZ
    GZACAFGZUMUNGZFZUPUKURACBHIURUSADFZDBFZGZFZVAACGZVDVEURVFUSVDACJKUQCABCLMVB
    ABGZVCURADVGVCAVGBDABNOPUQBABCQMRSVDUTUSVDVCTVBTUMUNVBVCUADBUBADUBUCUDUEUSU
    LUTUOULUSAACHUFUTUOUMUNUGIUHUIUKUOULHUJ $.
    $( [17-Aug-2011] $) $( [14-Aug-2011] $)

  $( ? $)
  arg-ax $p |- ( ( ph -/\ ( ps -/\ ch ) ) -/\ ( ( ph -/\ ( ps -/\ ch ) )
          -/\ ( ( th -/\ ch ) -/\ ( ( ch -/\ th ) -/\ ( ph -/\ th ) ) ) ) ) $=
    ( wnand wa wi wn wo orel2 com12 pm3.27 a1i jad pm3.45 anim12i jaob 3imtr4i
    pm3.22 imim2i 3syl pm4.57 syl5ib con1d df-nand biimpri syl6 nic-justlem
    ancli mpbir ) ABCEEZUKDCEZCDEZADEZEEZEEUKUKUOFGUKUOABCFZGZULUMUNFZGUKUOUQDC
    FZHZURULUQUTCDFZHZADFZHZFZURUQVEUSUQVAVCIZUSVEHUQCAIZCGZVFVAGZVFUSGVGUQCVGA
    UPCAHVGCACJKUPCGVGBCLMNKCCGZACGZFVAVAGZVCVAGZFVHVIVJVLVKVMCCDOACDOPCCAQVAVA
    VCQRVAUSVFCDSTUAVAVCUBUCUDVBUMVDUNUMVBCDUEUFUNVDADUEUFPUGDCUEUCACBUHULUNUMU
    HRUIUKUOUKUHUJ $.
    $( [17-Aug-2011] $) $( [14-Aug-2011] $)

