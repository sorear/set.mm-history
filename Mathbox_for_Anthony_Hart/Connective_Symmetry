$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Connective Symmetry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( In the paper "On Variable Functors of Propositional Arguments",
     Lukasiewicz introduced a system that can handle variable connectives.
     This was done by introducing a variable, marked with a lowercase delta,
     which takes a wff as input.  In the system, "delta ` ph ` " means that
     "something is true of ` ph ` ."  "delta ` ph ` " can be substituted with
     ` -. ph ` , ` ps /\ ph ` , ` A. x ph ` , etc.

     Later on, Meredith discovered a single axiom, in the form of ` ( ` delta
     delta ` F. -> ` delta ` ph ` ` ) ` .  This represents the shortest theorem
     in the extended propositional calculus that cannot be derived as an
     instance of a theorem in propositional calculus.

     A symmetry with ` -. ` . $)
  negsym1 $p |- ( -. -. F. -> -. ph ) $=
    ( wfal wn notfal pm2.24i ) BCACDE $.
    $( [13-Sep-2011] $) $( [4-Sep-2011] $)

  $( A symmetry with ` -> ` .

     See ~ negsym1 for more information. $)
  imsym1 $p |- ( ( ps -> ( ps -> F. ) ) -> ( ps -> ph ) ) $=
    ( wfal wi pm2.21 FiA imim2i ja ) BBCDBADBAECABAFGH $.
    $( [13-Sep-2011] $) $( [4-Sep-2011] $)

  $( A symmetry with ` <-> ` .

     See ~ negsym1 for more information. $)
  bisym1 $p |- ( ( ps <-> ( ps <-> F. ) ) -> ( ps <-> ph ) ) $=
    ( wfal wb wn falvar bibi2i pm5.19 pm2.21i sylbir ) BBCDZDBBEZDZBADZLKBBFGMN
    BHIJ $.
    $( [13-Sep-2011] $) $( [4-Sep-2011] $)

  $( A symmetry with ` /\ ` .

     See ~ negsym1 for more information. $)
  consym1 $p |- ( ( ps /\ ( ps /\ F. ) ) -> ( ps /\ ph ) ) $=
    ( wfal wa wi FiA ad2antll pm2.43i ) BBCDDZBADZCIJEZBBKFGH $.
    $( [13-Sep-2011] $) $( [4-Sep-2011] $)

  $( A symmetry with ` \/ ` .

     See ~ negsym1 for more information. $)
  dissym1 $p |- ( ( ps \/ ( ps \/ F. ) ) -> ( ps \/ ph ) ) $=
    ( wo wfal orc FiA orim2i jaoi ) BBACBDCBAEDABAFGH $.
    $( [13-Sep-2011] $) $( [4-Sep-2011] $)

  $( A symmetry with ` -/\ ` .

     See ~ negsym1 for more information. $)
  nandsym1 $p |- ( ( ps -/\ ( ps -/\ F. ) ) -> ( ps -/\ ph ) ) $=
    ( wfal wnand wa wn df-nand biimpi anbi2i biimpri nsyl simpl notfal intnan
    jctir con3i 3syl ) BBCDZDZBBCEFZEZFBAEZFZBADZSBREZUASUEFBRGHUEUARTBBCGIJKUB
    UAUBBTBALCBMNOPUDUCBAGJQ $.
    $( [13-Sep-2011] $) $( [4-Sep-2011] $)

  $( A symmetry with ` A. ` .

     See ~ negsym1 for more information. $)
  unisym1 $p |- ( A. x A. x F. -> A. x ph ) $=
    ( wfal wal wi FiA weq cbv1 pm2.43i ) CBDZBDZABDZCJABBJKEFALEFBBGJAEEFHI $.
    $( [13-Sep-2011] $) $( [4-Sep-2011] $)

  $( A symmetry with ` E. ` .

     See ~ negsym1 for more information. $)
  exisym1 $p |- ( E. x E. x F. -> E. x ph ) $=
    ( wfal wex hbe1 FiA eximi 19.23ai ) CBDABDBABECABAFGH $.
    $( [13-Sep-2011] $) $( [4-Sep-2011] $)

  $( A symmetry with ` E! ` .

     See ~ negsym1 for more information. $)
  unqsym1 $p |- ( E! x E! x F. -> E! x ph ) $=
    ( wfal weu wex unnf nex euex mto pm2.21i ) CBDZBDZABDLKBEKBBFGKBHIJ $.
    $( [13-Sep-2011] $) $( [6-Sep-2011] $)

  $( A symmetry with ` E* ` .

     See ~ negsym1 for more information. $)
  amosym1 $p |- ( E* x E* x F. -> E* x ph ) $=
    ( wfal wmo wex weu wi df-mo wn mof 19.8a pm2.24d pm2.01d ax-mp pm2.21i
    notnoti nex eunex mto ja sylbi ) CBDZBDUBBEZUBBFZGABDZUBBHUCUDUEUCIZUEUBUFI
    ZBJZUBUFUBUCUGUBBKLMNOUDUEUDUBIZBEUIBUBUHPQUBBRSOTUA $.
    $( [13-Sep-2011] $)

  $( A symmetry with ` [ x / y ] ` .

     See ~ negsym1 for more information. $)
  subsym1 $p |- ( [ x / y ] [ x / y ] F. -> [ x / y ] ph ) $=
    ( wfal wsb cv wceq wi wa wex notfal intnan nex df-sb mtbir pm2.21i ) DCBEZC
    BEZACBERCFBFGZQHZSQIZCJZIUBTUACQSQSDHZSDIZCJZIUEUCUDCDSKLMLDCBNOLMLQCBNOP
    $.
    $( [13-Sep-2011] $) $( [11-Sep-2011] $)

$( (End of Anthony Hart's mathbox.) $)


