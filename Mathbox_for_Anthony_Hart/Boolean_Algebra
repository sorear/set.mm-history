$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Boolean Algebra
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c T. $.
  $c F. $.

  $( ` T. ` is a wff. $)
  wtru $a wff T. $.

  $( ` F. ` is a wff. $)
  wfal $a wff F. $.

  $( Definition of ` T. ` , a tautology. ` T. ` is a constant true.  In this
     definition ~ biid is used as an antecedent, however, any true wff, such as
     an axiom, can be used in its place. $)
  df-tru $a |- ( T. <-> ( ph <-> ph ) ) $.

  $( Definition of ` F. ` , a contradiction. ` F. ` is a constant false. $)
  df-fal $a |- ( F. <-> -. T. ) $.

  $( ` T. ` is true. $)
  trutru $p |- T. $=
    ( wph wtru wb biid df-tru mpbir ) BAACADAEF $.
    $( [23-Oct-2010] $) $( [13-Oct-2010] $)

  $( ` F. ` is not true. $)
  nofaltru $p |- -. F. $=
    ( wtru wfal wn trutru df-fal con2bii mpbi ) ABCDBAEFG $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  ${
    truvar.1 $e |- ph $.
    $( If something is true, it outputs ` T. ` . $)
    truvar $p |- ( ph <-> T. ) $=
      ( wtru trutru 2th ) ACBDE $.
      $( [23-Oct-2010] $) $( [22-Oct-2010] $)
  $}

  ${
    falvar.1 $e |- -. ph $.
    $( If something is not true, it outputs ` F. ` . $)
    falvar $p |- ( ph <-> F. ) $=
      ( wfal wn nofaltru 2th con4bii ) ACADCDBEFG $.
      $( [23-Oct-2010] $) $( [22-Oct-2010] $)
  $}

  $( A ` /\ ` identity. $)
  TTAi $p |- ( ( T. /\ T. ) <-> T. ) $=
    ( wtru anidm ) AB $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` /\ ` identity. $)
  TFAi $p |- ( ( T. /\ F. ) <-> F. ) $=
    ( wtru wfal wa wn nofaltru intnan 2th con4bii ) ABCZBIDBDBAEFEGH $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` /\ ` identity. $)
  FTAi $p |- ( ( F. /\ T. ) <-> F. ) $=
    ( wfal wtru wa wn nofaltru intnanr 2th con4bii ) ABCZAIDADABEFEGH $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` /\ ` identity. $)
  FFAi $p |- ( ( F. /\ F. ) <-> F. ) $=
    ( wfal anidm ) AB $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` \/ ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  TTOi $p |- ( ( T. \/ T. ) <-> T. ) $=
    ( wtru oridm ) AB $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` \/ ` identity. $)
  TTOiOLD $p |- ( ( T. \/ T. ) <-> T. ) $=
    ( wtru wo trutru orci 2th ) AABAAACDCE $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` \/ ` identity. $)
  TFOi $p |- ( ( T. \/ F. ) <-> T. ) $=
    ( wtru wfal wo trutru orci 2th ) ABCAABDEDF $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` \/ ` identity. $)
  FTOi $p |- ( ( F. \/ T. ) <-> T. ) $=
    ( wfal wtru wo trutru olci 2th ) ABCBBADEDF $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` \/ ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  FFOi $p |- ( ( F. \/ F. ) <-> F. ) $=
    ( wfal oridm ) AB $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` \/ ` identity. $)
  FFOiOLD $p |- ( ( F. \/ F. ) <-> F. ) $=
    ( wfal wo wn wa nofaltru pm3.2i pm4.56 mpbi 2th con4bii ) AABZAKCZACZMMDLMM
    EEFAAGHEIJ $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` -> ` identity. $)
  TTIi $p |- ( ( T. -> T. ) <-> T. ) $=
    ( wtru wi trutru a1i 2th ) AABAAACDCE $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` -> ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  TFIi $p |- ( ( T. -> F. ) <-> F. ) $=
    ( wfal wtru wi trutru a1bi bicomi ) ABACBADEF $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` -> ` identity. $)
  TFIiOLD $p |- ( ( T. -> F. ) <-> F. ) $=
    ( wtru wfal wi wn wa trutru nofaltru pm3.2i annim mpbi 2th con4bii ) ABCZBM
    DZBDZAOENAOFGHABIJGKL $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` -> ` identity. $)
  FTIi $p |- ( ( F. -> T. ) <-> T. ) $=
    ( wfal wtru wi trutru a1i 2th ) ABCBBADEDF $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` -> ` identity. $)
  FFIi $p |- ( ( F. -> F. ) <-> T. ) $=
    ( wfal wi wtru id trutru 2th ) AABCADEF $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` -. ` identity. $)
  TNi $p |- ( -. T. <-> F. ) $=
    ( wfal wtru wn df-fal bicomi ) ABCDE $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` -. ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  FNi $p |- ( -. F. <-> T. ) $=
    ( wtru wfal TNi con1bii ) ABCD $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` -. ` identity. $)
  FNiOLD $p |- ( -. F. <-> T. ) $=
    ( wfal wn wtru trutru a1i nofaltru impbii ) ABZCCHDEHCFEG $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` <-> ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  TTBi $p |- ( ( T. <-> T. ) <-> T. ) $=
    ( wtru wb df-tru bicomi ) AAABACD $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` <-> ` identity. $)
  TTBiOLD $p |- ( ( T. <-> T. ) <-> T. ) $=
    ( wtru wb trutru 2th ) AABAAACCDCD $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` <-> ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  TFBi $p |- ( ( T. <-> F. ) <-> F. ) $=
    ( wtru wfal wb wn TNi nbbn mpbi nofaltru 2false ) ABCZBADBCJDEABFGHI $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` <-> ` identity. $)
  TFBiOLD $p |- ( ( T. <-> F. ) <-> F. ) $=
    ( wtru wfal wb wn TNi nbbn mpbi nofaltru 2th con4bii ) ABCZBKDZBDADBCLEABFG
    HIJ $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` <-> ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  FTBi $p |- ( ( F. <-> T. ) <-> F. ) $=
    ( wfal wtru wb bicom TFBi bitri ) ABCBACAABDEF $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` <-> ` identity. $)
  FTBiOLD $p |- ( ( F. <-> T. ) <-> F. ) $=
    ( wfal wtru wb wn FNi nbbn mpbi nofaltru 2th con4bii ) ABCZAKDZADZMBCLEABFG
    HIJ $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` <-> ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  FFBi $p |- ( ( F. <-> F. ) <-> T. ) $=
    ( wtru wfal wb df-tru bicomi ) ABBCBDE $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` <-> ` identity. $)
  FFBiOLD $p |- ( ( F. <-> F. ) <-> T. ) $=
    ( wfal wb wtru biid trutru 2th ) AABCADEF $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` -/\ ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  TTSi $p |- ( ( T. -/\ T. ) <-> F. ) $=
    ( wtru wnand wn wfal nic-justneg TNi bitr3i ) AABACDAEFG $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` -/\ ` identity. $)
  TTSiOLD $p |- ( ( T. -/\ T. ) <-> F. ) $=
    ( wtru wnand wfal wn wa trutru TTAi mpbir df-nand con2bii mpbi nofaltru
    2th con4bii ) AABZCODZCDAAEZPQAFGHOQAAIJKLMN $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

  $( A ` -/\ ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  TFSi $p |- ( ( T. -/\ F. ) <-> T. ) $=
    ( wtru wfal wnand wa wn df-nand TFAi notbii FNi 3bitri ) ABCABDZEBEAABFKBGH
    IJ $.
    $( [16-May-2011] $) $( [23-Oct-2010] $)

  $( A ` -/\ ` identity. $)
  TFSiOLD $p |- ( ( T. -/\ F. ) <-> T. ) $=
    ( wtru wfal wnand wa wn trutru TFAi TNi bicomi bitri con2bii mpbir df-nand
    2th ) ABCZAOABDZEZQAFAQPAPBAEZGRBHIJKILABMLFN $.
    $( [23-Oct-2010] $) $( [23-Oct-2010] $)

  $( A ` -/\ ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  FTSi $p |- ( ( F. -/\ T. ) <-> T. ) $=
    ( wfal wtru wnand wa wn df-nand FTAi notbii FNi 3bitri ) ABCABDZEAEBABFKAGH
    IJ $.
    $( [16-May-2011] $) $( [23-Oct-2010] $)

  $( A ` -/\ ` identity. $)
  FTSiOLD $p |- ( ( F. -/\ T. ) <-> T. ) $=
    ( wfal wtru wnand wa wn trutru FTAi TNi bicomi bitri con2bii mpbir df-nand
    2th ) ABCZBOABDZEZQBFBQPBPABEZGRAHIJKILABMLFN $.
    $( [23-Oct-2010] $) $( [23-Oct-2010] $)

  $( A ` -/\ ` identity.  (The proof was shortened by Andrew Salmon,
     13-May-2011.) $)
  FFSi $p |- ( ( F. -/\ F. ) <-> T. ) $=
    ( wfal wnand wn wtru nic-justneg FNi bitr3i ) AABACDAEFG $.
    $( [16-May-2011] $) $( [22-Oct-2010] $)

  $( A ` -/\ ` identity. $)
  FFSiOLD $p |- ( ( F. -/\ F. ) <-> T. ) $=
    ( wfal wnand wtru wa wn trutru FFAi TNi bicomi bitri con2bii mpbi df-nand
    mpbir 2th ) AABZCPAADZEZCRFQCQACEZGSAHIJKLAAMNFO $.
    $( [23-Oct-2010] $) $( [22-Oct-2010] $)

$( (End of Anthony Hart's mathbox.) $)


