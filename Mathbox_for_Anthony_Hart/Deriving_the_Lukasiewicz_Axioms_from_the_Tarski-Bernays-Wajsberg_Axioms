$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      Deriving the Lukasiewicz Axioms from the Tarski-Bernays-Wajsberg Axioms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Justification for ~ tbw-negdf . $)
  tbw-bijust $p |- ( ( ph <-> ps ) <-> ( ( ( ph -> ps )
    -> ( ( ps -> ph ) -> F. ) ) -> F. ) ) $=
    ( wb wi wn wfal dfbi1 pm2.21 imim2i id FiA ja impbii notbii ax-1 pm2.43i
    3bitri ) ABCABDZBADZEZDZERSFDZDZEZUCFDZABGUAUCUAUCTUBRSFHIUBTRSFTTJTKLIMNUD
    UEUCFHUEUDUCFUEUDDZUDUEOUFKLPMQ $.
    $( [17-Aug-2011] $) $( [15-Aug-2011] $)

  $( The definition of negation, in terms of ` -> ` and ` F. ` . $)
  tbw-negdf $p |- ( ( ( -. ph -> ( ph -> F. ) )
    -> ( ( ( ph -> F. ) -> -. ph ) -> F. ) ) -> F. ) $=
    ( wn wfal wi wb pm2.21 ax-1 FiA ja pm2.43i impbii tbw-bijust mpbi ) ABZACDZ
    ENODONDZCDDCDNOACFONACPNOGPHIJKNOLM $.
    $( [17-Aug-2011] $) $( [15-Aug-2011] $)

  $( The first of four axioms in the Tarski-Bernays-Wajsberg system. $)
  tbw-ax1 $p |- ( ( ph -> ps ) -> ( ( ps -> ch ) -> ( ph -> ch ) ) ) $=
    ( imim1 ) ABCD $.
    $( [17-Aug-2011] $) $( [13-Aug-2011] $)

  $( The second of four axioms in the Tarski-Bernays-Wajsberg system. $)
  tbw-ax2 $p |- ( ph -> ( ps -> ph ) ) $=
    ( ax-1 ) ABC $.
    $( [17-Aug-2011] $) $( [13-Aug-2011] $)

  $( The third of four axioms in the Tarski-Bernays-Wajsberg system. $)
  tbw-ax3 $p |- ( ( ( ph -> ps ) -> ph ) -> ph ) $=
    ( peirce ) ABC $.
    $( [17-Aug-2011] $) $( [13-Aug-2011] $)

  $( The fourth of four axioms in the Tarski-Bernays-Wajsberg system.

     This axiom was added to the Tarski-Bernays axiom system ( see ~ tb-ax1 ,
     ~ tb-ax2 , and ~ tb-ax3 ) by Wajsberg for completeness. $)
  tbw-ax4 $p |- ( F. -> ph ) $=
    ( FiA ) AB $.
    $( [17-Aug-2011] $) $( [13-Aug-2011] $)

  ${
    tbwsyl.1 $e |- ( ph -> ps ) $.
    tbwsyl.2 $e |- ( ps -> ch ) $.
    $( Used to rederive the Lukasiewicz axioms from
       Tarski-Bernays-Wajsberg'. $)
    tbwsyl $p |- ( ph -> ch ) $=
      ( wi tbw-ax1 ax-mp ) BCFZACFZEABFIJFDABCGHH $.
      $( [17-Aug-2011] $) $( [16-Aug-2011] $)
  $}

  $( Used to rederive the Lukasiewicz axioms from Tarski-Bernays-Wajsberg'. $)
  tbwlem1 $p |- ( ( ph -> ( ps -> ch ) ) -> ( ps -> ( ph -> ch ) ) ) $=
    ( wi tbw-ax2 tbw-ax1 tbwsyl tbw-ax3 mpsyl ) BBCDZCDZDAJDKACDZDBLDBJKDZKBJBD
    MBJEJBCFGMKCDKDKJKCFKCHGGAJCFBKLFI $.
    $( [17-Aug-2011] $) $( [16-Aug-2011] $)

  $( Used to rederive the Lukasiewicz axioms from Tarski-Bernays-Wajsberg'. $)
  tbwlem2 $p |- ( ( ph -> ( ps -> F. ) ) -> ( ( ( ph -> ch ) -> th )
    -> ( ps -> th ) ) ) $=
    ( wfal wi tbw-ax4 tbw-ax1 tbwlem1 ax-mp mpsyl tbwsyl ) ABEFZFZBACFZFZODFBDF
    FBMCFZFZNQOFPMBCFZFZRECFZTCGMUASFFUATFBECHMUASIJJMBCIJAMCHBQOHKBODHL $.
    $( [17-Aug-2011] $) $( [16-Aug-2011] $)

  $( Used to rederive the Lukasiewicz axioms from Tarski-Bernays-Wajsberg'. $)
  tbwlem3 $p |- ( ( ( ( ( ph -> F. ) -> ph ) -> ph ) -> ps ) -> ps ) $=
    ( wfal wi tbw-ax3 tbw-ax2 tbw-ax1 tbwsyl ax-mp ) ACDADADZBDZKBDZDZLJMACEJKJ
    DMJKFKJBGHIMLBDLDLKLBGLBEHI $.
    $( [17-Aug-2011] $) $( [16-Aug-2011] $)

  $( Used to rederive the Lukasiewicz axioms from Tarski-Bernays-Wajsberg'. $)
  tbwlem4 $p |- ( ( ( ph -> F. ) -> ps ) -> ( ( ps -> F. ) -> ph ) ) $=
    ( wfal wi tbw-ax4 tbw-ax1 tbwlem1 ax-mp tbwlem2 tbwlem3 tbwsyl ) ACDZBDZLBC
    DZCDZDZNADZBODZMPDZNNDZRCCDZTCENUANDDUATDBCCFNUANGHHNBCGHMRPDDRSDLBOFMRPGHH
    PLADADQDQLNAAIAQJKK $.
    $( [17-Aug-2011] $) $( [16-Aug-2011] $)

  $( Used to rederive the Lukasiewicz axioms from Tarski-Bernays-Wajsberg'. $)
  tbwlem5 $p |- ( ( ( ph -> ( ps -> F. ) ) -> F. ) -> ph ) $=
    ( wfal wi tbw-ax2 tbw-ax1 tbwsyl tbwlem1 ax-mp tbwlem4 ) ACDZABCDZDZDZMCDAD
    AKLDZDNABADOABEBACFGAKLHIAMJI $.
    $( [17-Aug-2011] $) $( [16-Aug-2011] $)

  $( ~ luk-1 derived from the Tarski-Bernays-Wajsberg axioms. $)
  re1luk1 $p |- ( ( ph -> ps ) -> ( ( ps -> ch ) -> ( ph -> ch ) ) ) $=
    ( tbw-ax1 ) ABCD $.
    $( [17-Aug-2011] $) $( [16-Aug-2011] $)

  $( ~ luk-2 derived from the Tarski-Bernays-Wajsberg axioms. $)
  re1luk2 $p |- ( ( -. ph -> ph ) -> ph ) $=
    ( wn wi wfal tbw-negdf tbw-ax2 tbwlem4 ax-mp tbw-ax1 tbw-ax3 tbwsyl ) ABZAC
    ZADCZACZANLCZMOCLNCZPDCZCZDCZPAERSCTPCRQFPSGHHNLAIHADJK $.
    $( [17-Aug-2011] $) $( [16-Aug-2011] $)

  $( ~ luk-3 derived from the Tarski-Bernays-Wajsberg axioms.

     This theorem, along with ~ re1luk1 and ~ re1luk2 proves that ~ tbw-ax1 ,
     ~ tbw-ax2 , ~ tbw-ax3 , and ~ tbw-ax4 , with ~ ax-mp can be used as a
     complete axiom system for all of propositional calculus. $)
  re1luk3 $p |- ( ph -> ( -. ph -> ps ) ) $=
    ( wn wfal wi tbw-negdf tbwlem5 ax-mp tbw-ax4 tbw-ax1 tbwlem1 mpsyl ) ACZADE
    ZEZANBEZMBEONMEZDEEDEOAFOQGHNABEZEZAPEDBEZSBINTREETSEADBJNTRKHHNABKHMNBJL
    $.
    $( [17-Aug-2011] $) $( [16-Aug-2011] $)

