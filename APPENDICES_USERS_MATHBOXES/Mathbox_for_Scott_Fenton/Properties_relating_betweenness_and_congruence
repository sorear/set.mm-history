$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Properties relating betweenness and congruence
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c InnerFiveSeg Cgr3 Colinear FiveSeg $.

  $( Declare the syntax for the inner five segment predicate. $)
  cifs $a class InnerFiveSeg $.

  $( Declare the syntax for the three place congruence predicate. $)
  ccgr3 $a class Cgr3 $.

  $( Declare the syntax for the colinearity predicate. $)
  ccolin $a class Colinear $.

  $( Declare the syntax for the five segment predicate. $)
  cfs $a class FiveSeg $.

  ${
    $d a b c d x y z w p q n $.
    $( The inner five segment configuration is an abbreviation for another
       congruence condition.  See ~ brifs and ~ ifscgr for how it is used.
       Definition 4.1 of [Schwabhauser] p. 34. $)
    df-ifs $a |- InnerFiveSeg =
        { <. p , q >. |
           E. n e. NN E. a e. ( EE ` n ) E. b e. ( EE ` n ) E. c e. ( EE ` n )
           E. d e. ( EE ` n ) E. x e. ( EE ` n ) E. y e. ( EE ` n )
           E. z e. ( EE ` n ) E. w e. ( EE ` n ) (
           p = <. <. a , b >. , <. c , d >. >. /\
           q = <. <. x , y >. , <. z , w >. >. /\
           ( ( b Btwn <. a , c >. /\ y Btwn <. x , z >. ) /\
             ( <. a , c >. Cgr <. x , z >. /\
               <. b , c >. Cgr <. y , z >. ) /\
             ( <. a , d >. Cgr <. x , w >. /\
               <. c , d >. Cgr <. z , w >. ) ) ) } $.
  $}

  ${
    $d a b c d e f n p q $.
    $( The three place congruence predicate.  This is an abbreviation for
       saying that all three pair in a triple are congruent with each other.
       Three place form of Definition 4.4 of [Schwabhauser] p. 35. $)
    df-cgr3 $a |- Cgr3 = { <. p , q >. |
       E. n e. NN E. a e. ( EE ` n ) E. b e. ( EE ` n ) E. c e. ( EE ` n )
       E. d e. ( EE ` n ) E. e e. ( EE ` n ) E. f e. ( EE ` n )
       ( p = <. a , <. b , c >. >. /\
       q = <. d , <. e , f >. >. /\
       ( <. a , b >. Cgr <. d , e >. /\ <. a , c >. Cgr <. d , f >. /\
         <. b , c >. Cgr <. e , f >. ) ) } $.
  $}

  ${
    $d a b c n $.
    $( The colinearity predicate states that the three points in its arguments
       sit on one line.  Definition 4.10 of [Schwabhauser] p. 36. $)
    df-colinear $a |- Colinear = `' { <. <. b , c >. , a >. |
       E. n e. NN ( ( a e. ( EE ` n ) /\ b e. ( EE ` n ) /\ c e. ( EE ` n ) )
         /\
         ( a Btwn <. b , c >. \/ b Btwn <. c , a >. \/
           c Btwn <. a , b >. ) ) } $.
  $}

  ${
    $d a b c d x y z w p q n $.
    $( The general five segment configuration is a generalization of the outer
       and inner five segment configurations.  See ~ brfs and ~ fscgr for its
       use.  Definition 4.15 of [Schwabhauser] p. 37. $)
    df-fs $a |- FiveSeg =
        { <. p , q >. |
           E. n e. NN E. a e. ( EE ` n ) E. b e. ( EE ` n ) E. c e. ( EE ` n )
           E. d e. ( EE ` n ) E. x e. ( EE ` n ) E. y e. ( EE ` n )
           E. z e. ( EE ` n ) E. w e. ( EE ` n ) (
           p = <. <. a , b >. , <. c , d >. >. /\
           q = <. <. x , y >. , <. z , w >. >. /\
           ( a Colinear <. b , c >. /\
             <. a , <. b , c >. >. Cgr3 <. x , <. y , z >. >. /\
             ( <. a , d >. Cgr <. x , w >. /\
               <. b , d >. Cgr <. y , w >. ) ) ) } $.
  $}

  ${
    $d N a b c d e f g h p q n $.  $d A a b c d e f g h p q n $.
    $d B a b c d e f g h p q n $.  $d C a b c d e f g h p q n $.
    $d D a b c d e f g h p q n $.  $d E a b c d e f g h p q n $.
    $d F a b c d e f g h p q n $.  $d G a b c d e f g h p q n $.
    $d H a b c d e f g h p q n $.
    $( Binary relationship form of the inner five segment predicate.
       (Contributed by Scott Fenton, 26-Sep-2013.) $)
    brifs $p |- ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
        ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
    ( <. <. A , B >. , <. C , D >. >. InnerFiveSeg
      <. <. E , F >. , <. G , H >. >. <-> ( ( B Btwn <. A , C >. /\
                     F Btwn <. E , G >. ) /\
        ( <. A , C >. Cgr <. E , G >. /\
          <. B , C >. Cgr <. F , G >. ) /\
        ( <. A , D >. Cgr <. E , H >. /\
          <. C , D >. Cgr <. G , H >. ) ) ) ) $=
      ( cv cop cbtwn wbr wa ccgr w3a wceq opeq1 breq2d breq1d vb va vc vf ve vg
      vd vh vn vq vp cee cifs cn anbi1d 3anbi123d breq1 anbi2d 3anbi12d anbi12d
      cfv opeq2 3anbi3d fveq2 df-ifs br8 ) UAJZUBJZUCJZKZLMZUDJZUEJZUFJZKZLMZNZ
      VJVOOMZVGVIKZVLVNKZOMZNZVHUGJZKZVMUHJZKZOMZVIWCKZVNWEKZOMZNZPVGAVIKZLMZVP
      NZWLVOOMZWANZAWCKZWFOMZWJNZPBWLLMZVPNZWOBVIKZVTOMZNZWSPBACKZLMZVPNZXEVOOM
      ZBCKZVTOMZNZWRCWCKZWIOMZNZPXGXKADKZWFOMZCDKZWIOMZNZPXFVLEVNKZLMZNZXEXTOMZ
      XJNZXOEWEKZOMZXRNZPXFFXTLMZNZYCXIFVNKZOMZNZYGPXFFEGKZLMZNZXEYMOMZXIFGKZOM
      ZNZYFXQGWEKZOMZNZPYOYSXOEHKZOMZXQGHKZOMZNZPUIABCDUIJZULVAIULVAUMUNUEUDUFU
      HEFGHIUJUKUBUAUCUGVHAQZVQWNWBWPWKWSUUIVKWMVPUUIVJWLVGLVHAVIRZSUOUUIVRWOWA
      UUIVJWLVOOUUJTUOUUIWGWRWJUUIWDWQWFOVHAWCRTUOUPVGBQZWNXAWPXDWSUUKWMWTVPVGB
      WLLUQUOUUKWAXCWOUUKVSXBVTOVGBVIRTURUSVICQZXAXGXDXKWSXNUULWTXFVPUULWLXEBLV
      ICAVBZSUOUULWOXHXCXJUULWLXEVOOUUMTUULXBXIVTOVICBVBTUTUULWJXMWRUULWHXLWIOV
      ICWCRTURUPWCDQZXNXSXGXKUUNWRXPXMXRUUNWQXOWFOWCDAVBTUUNXLXQWIOWCDCVBTUTVCV
      MEQZXGYBXKYDXSYGUUOVPYAXFUUOVOXTVLLVMEVNRZSURUUOXHYCXJUUOVOXTXEOUUPSUOUUO
      XPYFXRUUOWFYEXOOVMEWERSUOUPVLFQZYBYIYDYLYGUUQYAYHXFVLFXTLUQURUUQXJYKYCUUQ
      VTYJXIOVLFVNRSURUSVNGQZYIYOYLYSYGUUBUURYHYNXFUURXTYMFLVNGEVBZSURUURYCYPYK
      YRUURXTYMXEOUUSSUURYJYQXIOVNGFVBSUTUURXRUUAYFUURWIYTXQOVNGWERSURUPWEHQZUU
      BUUGYOYSUUTYFUUDUUAUUFUUTYEUUCXOOWEHEVBSUUTYTUUEXQOWEHGVBSUTVCUUHIULVDUEU
      DUFUHUIUJUKUBUAUCUGVEVF $.
      $( [14-Oct-2013] $) $( [26-Sep-2013] $)
  $}

  ${
    $d A e f $.  $d B e f $.  $d C e f $.  $d D e f $.  $d E e f $.
    $d F e f $.  $d G e f $.  $d H e f $.  $d N e f $.
    $( Inner five segment congruence.  Take two triangles, ` A D C ` and
       ` E H G ` , with ` B ` between ` A ` and ` C ` and ` F ` between ` E `
       and ` G ` .  If the other components of the triangles are congruent,
       then so are ` B D ` and ` F H ` .  Theorem 4.2 of [Schwabhauser] p. 34.
       (Contributed by Scott Fenton, 27-Sep-2013.) $)
    ifscgr $p |- ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
        ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
    ( <. <. A , B >. , <. C , D >. >. InnerFiveSeg
      <. <. E , F >. , <. G , H >. >. -> <. B , D >. Cgr <. F , H >. ) ) $=
      ( wcel w3a cop wbr cbtwn wa ccgr wi syl122anc wb adantr ve vf cn cee cifs
      cfv brifs wceq simp1l simp11 simp13 simp21 axbtwnid syl3anc simp2r simp3r
      syl5 jca opeq2 breq1d opeq1 anbi12d biimprd simp31 simp32 cgrid2 syl13anc
      mpan9 breq2d syl6 imp3a exp3a mpdd anbi1d 3anbi12d imbi1d syl5ibr cv wrex
      simp12 btwndiff simpl11 simpl23 simpl32 simpl21 simpr axsegcon anass cofs
      wne simplrl adantl simplll simpr2l simpllr ad2antrr simplrr mpbid simprr3
      cgrcom 3jca simpl12 simprl simpl22 simprr simpl33 brofs syl333anc sylibrd
      ex 5segofs syland simpr1l simpr1r jca32 simpl13 btwnexch3 simpl31 anim12d
      imp btwncom ad2antrl cgrcomlr bitrd simpr2r simpr3r a1i jcad syld adantrd
      necomd syl5bir anassrs rexlimdva mpd com3r pm2.61ine sylbid ) IUCJZAIUDUF
      ZJZBYTJZKZCYTJZDYTJZEYTJZKZFYTJZGYTJZHYTJZKZKZABLCDLZLEFLGHLZLUEMBACLZNMZ
      FEGLZNMZOZUUOUUQPMZBCLZFGLZPMZOZADLEHLPMZUUMUUNPMZOZKZBDLZFHLZPMZABCDEFGH
      IUGUULUVHUVKQZQACUULUVLACUHZBCCLZNMZUUROZUVNUUQPMZUVCOZUVGKZUVKQUULUVSBCU
      HZUVKUVSUVOUULUVTUVOUURUVRUVGUIUULYSUUBUUDUVOUVTQYSUUAUUBUUGUUKUJZYSUUAUU
      BUUGUUKUKZUUCUUDUUEUUFUUKULZBCIUMUNUQUULUVSUVTUVKUVSUVTOBBLZUVBPMZUVIUUNP
      MZOZUULUVKUVSUVCUVFOZUVTUWGUVSUVCUVFUVPUVQUVCUVGUOUVPUVRUVEUVFUPURUVTUWGU
      WHUVTUWEUVCUWFUVFUVTUWDUVAUVBPBCBUSUTUVTUVIUUMUUNPBCDVAUTVBVCVHUULUWEUWFU
      VKUULUWEFGUHZUWFUVKQUULYSUUBUUHUUIUWEUWIQUWAUWBUUCUUGUUHUUIUUJVDUUCUUGUUH
      UUIUUJVEZBFGIVFVGUWIUVKUWFUWIUVJUUNUVIPFGHVAVIVCVJVKUQVLVMUVMUVHUVSUVKUVM
      UUSUVPUVDUVRUVGUVMUUPUVOUURUVMUUOUVNBNACCVAZVIVNUVMUUTUVQUVCUVMUUOUVNUUQP
      UWKUTVNVOVPVQUULUVHACWJZUVKUULUVHUWLUVKUULCAUAVRZLNMZCUWMWJZOZUAYTVSZUVHU
      WLOZUVKQZUULYSUUAUUDUWQUWAYSUUAUUBUUGUUKVTUWCACIUAWAUNUULUWPUWSUAYTUULUWM
      YTJZOZUWPUWRUVKUXAGEUBVRZLNMZGUXBLZCUWMLZPMZOZUBYTVSZUWPUWROZUVKQZUXAYSUU
      FUUIUUDUWTUXHYSUUAUUBUUGUUKUWTWBUUDUUEUUFUUCUUKUWTWCUUHUUIUUJUUCUUGUWTWDU
      UDUUEUUFUUCUUKUWTWEUULUWTWFUBEGCUWMIWGRUXAUXGUXJUBYTUULUWTUXBYTJZUXGUXJQU
      ULUWTUXKOZOZUXGUXIUVKUXGUXIOUXGUWPOZUWROZUXMUVKUXGUWPUWRWHUXOUXNUVHOZUWLO
      ZUXMUVKUXNUVHUWLWHUXMUXQUWMDLZUXBHLZPMZUVKUXMUXPUUOUXRLUUQUXSLWIMZUWLUXTU
      XMUXPUWNUXCOZUUTUXEUXDPMZOZUVGKZUYAUXMUXPUYEUXMUXPOZUYBUYDUVGUYFUWNUXCUXP
      UWNUXMUXGUWNUWOUVHWKZWLUXPUXCUXMUXCUXFUWPUVHWMZWLURUYFUUTUYCUXPUUTUXMUUTU
      VCUUSUVGUXNWNWLUYFUXFUYCUXPUXFUXMUXCUXFUWPUVHWOZWLUYFYSUUIUXKUUDUWTUXFUYC
      SZUULYSUXLUXPUWAWPUULUUIUXLUXPUWJWPUULUWTUXKUXPWQUULUUDUXLUXPUWCWPUULUWTU
      XKUXPWKGUXBCUWMIWTZRWRURUUSUVDUVGUXNUXMWSXAXJUXMYSUUAUUDUWTUUEUUFUUIUXKUU
      JUYAUYESYSUUAUUBUUGUUKUXLWBZYSUUAUUBUUGUUKUXLXBZUUDUUEUUFUUCUUKUXLWEZUULU
      WTUXKXCZUUDUUEUUFUUCUUKUXLXDZUUDUUEUUFUUCUUKUXLWCZUUHUUIUUJUUCUUGUXLWDZUU
      LUWTUXKXEZUUHUUIUUJUUCUUGUXLXFZACUWMDEGUXBHIXGXHXIUXMYSUUAUUDUWTUUEUUFUUI
      UXKUUJUYAUWLOUXTQUYLUYMUYNUYOUYPUYQUYRUYSUYTACUWMDEGUXBHIXKXHXLUXMUXPUXTU
      VKQUWLUXMUXPUXTUVKUXMUXPUXTOZUWMCLZUVILUXBGLZUVJLWIMZUWMCWJZOZUVKUXMVUAVU
      DVUEUXMVUACUWMBLNMZGUXBFLNMZOZVUBVUCPMZCBLGFLPMZOZUXTUVFOZKZVUDUXMVUAVUNU
      XMVUAOZVUIVULVUMVUOCBUWMLNMZGFUXBLNMZOZVUIUXMVUAVURVUAUUPUWNOZUURUXCOZOUX
      MVURVUAVUSUURUXCVUAUUPUWNUXPUUPUXTUUPUURUVDUVGUXNXMTUXPUWNUXTUYGTURUXPUUR
      UXTUUPUURUVDUVGUXNXNTUXPUXCUXTUYHTXOUXMVUSVUPVUTVUQUXMYSUUAUUBUUDUWTVUSVU
      PQUYLUYMYSUUAUUBUUGUUKUXLXPZUYNUYOABCUWMIXQRUXMYSUUFUUHUUIUXKVUTVUQQUYLUY
      QUUHUUIUUJUUCUUGUXLXRZUYRUYSEFGUXBIXQRXSUQXTUXMVURVUISVUAUXMVUPVUGVUQVUHU
      XMYSUUDUUBUWTVUPVUGSUYLUYNVVAUYOCBUWMIYAVGUXMYSUUIUUHUXKVUQVUHSUYLUYRVVBU
      YSGFUXBIYAVGVBTWRVUOVUJVUKVUOUXFVUJUXPUXFUXMUXTUYIYBUXMUXFVUJSVUAUXMUXFUY
      CVUJUXMYSUUIUXKUUDUWTUYJUYLUYRUYSUYNUYOUYKRUXMYSUUDUWTUUIUXKUYCVUJSUYLUYN
      UYOUYRUYSCUWMGUXBIYCRYDTWRVUOUVCVUKUXPUVCUXMUXTUUTUVCUUSUVGUXNYEYBUXMUVCV
      UKSZVUAUXMYSUUBUUDUUHUUIVVCUYLVVAUYNVVBUYRBCFGIYCRTWRURVUOUXTUVFUXMUXPUXT
      XEUXPUVFUXMUXTUVEUVFUUSUVDUXNYFYBURXAXJUXMYSUWTUUDUUBUUEUXKUUIUUHUUJVUDVU
      NSUYLUYOUYNVVAUYPUYSUYRVVBUYTUWMCBDUXBGFHIXGXHXIVUAVUEQUXMVUACUWMUXPUWOUX
      TUXGUWNUWOUVHWQTYKYGYHUXMYSUWTUUDUUBUUEUXKUUIUUHUUJVUFUVKQUYLUYOUYNVVAUYP
      UYSUYRVVBUYTUWMCBDUXBGFHIXKXHYIVLYJVMYLYLVLYMYNYOVLYNYOVLYPYQYR $.
      $( [14-Oct-2013] $) $( [27-Sep-2013] $)
  $}

  $( Removing identical parts from the end of a line segment preserves
     congruence.  Theorem 4.3 of [Schwabhauser] p. 35.  (Contributed by Scott
     Fenton, 4-Oct-2013.) $)
  cgrsub $p |- ( ( N e. NN /\
         ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
         ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
         ( ( ( B Btwn <. A , C >. /\ E Btwn <. D , F >. ) /\
             ( <. A , C >. Cgr <. D , F >. /\ <. B , C >. Cgr <. E , F >. ) )
           -> <. A , B >. Cgr <. D , E >. ) ) $=
    ( cn wcel cee w3a cop cbtwn wbr wa ccgr wb cgrcomlr syl122anc mpbid simpl21
    cfv simprl simprr simpl1 simpl31 cgrtriv syl3anc simprrl simpl23 simpl33 wi
    jca 3jca simpl22 simpl32 cifs brifs ifscgr sylbird syl333anc mpd ex ) GHIZA
    GJUBZIZBVEIZCVEIZKZDVEIZEVEIZFVEIZKZKZBACLZMNEDFLZMNOZVOVPPNZBCLEFLPNZOZOZA
    BLZDELZPNZVNWAOZBALEDLPNZWDWEVQVTAALDDLPNZCALZFDLZPNZOZKZWFWEVQVTWKVNVQVTUC
    VNVQVTUDWEWGWJWEVDVFVJWGVDVIVMWAUEZVFVGVHVDVMWAUAZVJVKVLVDVIWAUFZADGUGUHWEV
    RWJVNVQVRVSUIWEVDVFVHVJVLVRWJQWMWNVFVGVHVDVMWAUJZWOVJVKVLVDVIWAUKZACDFGRSTU
    MUNWEVDVFVGVHVFVJVKVLVJWLWFULWMWNVFVGVHVDVMWAUOZWPWNWOVJVKVLVDVIWAUPZWQWOVD
    VFVGKVHVFVJKVKVLVJKKWLWBWHLWCWILUQNWFABCADEFDGURABCADEFDGUSUTVAVBWEVDVGVFVK
    VJWFWDQWMWRWNWSWOBAEDGRSTVC $.
    $( [14-Oct-2013] $) $( [4-Oct-2013] $)

  ${
    $d A a b c d e f n p q $.  $d B a b c d e f n p q $.
    $d C a b c d e f n p q $.  $d D a b c d e f n p q $.
    $d E a b c d e f n p q $.  $d F a b c d e f n p q $.
    $d N a b c d e f n p q $.
    $( Binary relationship form of the three-place congruence predicate.
       (Contributed by Scott Fenton, 4-Oct-2013.) $)
    brcgr3 $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. <->
         ( <. A , B >. Cgr <. D , E >. /\ <. A , C >. Cgr <. D , F >. /\
           <. B , C >. Cgr <. E , F >. ) ) ) $=
      ( va vb vd cv cop ccgr wbr w3a wceq opeq1 breq1d opeq2 breq2d ve vc vf vn
      vq vp cee cfv ccgr3 cn 3anbi12d 3anbi13d 3anbi23d fveq2 df-cgr3 br6 ) HKZ
      IKZLZJKZUAKZLZMNZUQUBKZLZUTUCKZLZMNZURVDLZVAVFLZMNZOAURLZVBMNZAVDLZVGMNZV
      KOABLZVBMNZVOBVDLZVJMNZOVQACLZVGMNZBCLZVJMNZOVPDVALZMNZVTDVFLZMNZWCOVPDEL
      ZMNZWGWBEVFLZMNZOWIVTDFLZMNZWBEFLZMNZOUDABCDUDKZUGUHGUGUHUIUJUAUCEFGUEUFH
      IUBJUQAPZVCVMVHVOVKWQUSVLVBMUQAURQRWQVEVNVGMUQAVDQRUKURBPZVMVQVKVSVOWRVLV
      PVBMURBASRWRVIVRVJMURBVDQRULVDCPZVOWAVSWCVQWSVNVTVGMVDCASRWSVRWBVJMVDCBSR
      UMUTDPZVQWEWAWGWCWTVBWDVPMUTDVAQTWTVGWFVTMUTDVFQTUKVAEPZWEWIWCWKWGXAWDWHV
      PMVAEDSTXAVJWJWBMVAEVFQTULVFFPZWGWMWKWOWIXBWFWLVTMVFFDSTXBWJWNWBMVFFESTUM
      WPGUGUNUAUCUDUEUFHIUBJUOUP $.
      $( [14-Oct-2013] $) $( [4-Oct-2013] $)
  $}


  $( Permutation law for three-place congruence.  (Contributed by Scott Fenton,
     5-Oct-2013.) $)
  cgr3permute3 $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. <->
         <. B , <. C , A >. >. Cgr3 <. E , <. F , D >. >. ) ) $=
    ( wcel w3a cop ccgr wbr ccgr3 wa wb 3simpa cgrcomlr syl3an 3simpb 3anrot cn
    cee cfv id 3anbi12d syl6bbr brcgr3 biid syl3anb 3bitr4d ) GUAHZAGUBUCZHZBUL
    HZCULHZIZDULHZEULHZFULHZIZIZABJDEJKLZACJDFJKLZBCJZEFJZKLZIZVFBAJEDJKLZCAJZF
    DJZKLZIZAVDJDVEJMLBVIJEVJJMLZVAVGVHVKVFIVLVAVBVHVCVKVFUKUKUPUMUNNUTUQURNVBV
    HOUKUDZUMUNUOPUQURUSPABDEGQRUKUKUPUMUONUTUQUSNVCVKOVNUMUNUOSUQURUSSACDFGQRU
    EVFVHVKTUFABCDEFGUGUKUKUPUNUOUMIUTURUSUQIVMVLOUKUHUMUNUOTUQURUSTBCAEFDGUGUI
    UJ $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)


  $( Permutation law for three-place congruence.  (Contributed by Scott Fenton,
     5-Oct-2013.) $)
  cgr3permute1 $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. <->
         <. A , <. C , B >. >. Cgr3 <. D , <. F , E >. >. ) ) $=
    ( cn wcel cee w3a cop ccgr wbr ccgr3 wa wb 3simpc brcgr3 3ancomb cfv syl3an
    id cgrcomlr 3anbi3d 3ancoma syl6bb biid syl3anb 3bitr4d ) GHIZAGJUAZIZBULIZ
    CULIZKZDULIZEULIZFULIZKZKZABLDELMNZACLDFLMNZBCLZEFLZMNZKZVCVBCBLZFELZMNZKZA
    VDLDVELONAVHLDVILONZVAVGVBVCVJKVKVAVFVJVBVCUKUKUPUNUOPUTURUSPVFVJQUKUCUMUNU
    ORUQURUSRBCEFGUDUBUEVBVCVJUFUGABCDEFGSUKUKUPUMUOUNKUTUQUSURKVLVKQUKUHUMUNUO
    TUQURUSTACBDFEGSUIUJ $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)


  $( Permutation law for three-place congruence.  (Contributed by Scott Fenton,
     5-Oct-2013.) $)
  cgr3permute2 $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. <->
         <. B , <. A , C >. >. Cgr3 <. E , <. D , F >. >. ) ) $=
    ( cn wcel cee cfv w3a cop ccgr3 wbr cgr3permute3 biid 3anrot cgr3permute1
    wb syl3anb bitrd ) GHIZAGJKZIZBUDIZCUDIZLZDUDIZEUDIZFUDIZLZLABCMMDEFMMNOBCA
    MMEFDMMNOZBACMMEDFMMNOZABCDEFGPUCUCUHUFUGUELULUJUKUILUMUNTUCQUEUFUGRUIUJUKR
    BCAEFDGSUAUB $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Permutation law for three-place congruence.  (Contributed by Scott Fenton,
     5-Oct-2013.) $)
  cgr3permute4 $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. <->
         <. C , <. A , B >. >. Cgr3 <. F , <. D , E >. >. ) ) $=
    ( cn wcel cee cfv w3a cop ccgr3 wbr cgr3permute3 wb biid 3anrot syl3anb
    bitrd ) GHIZAGJKZIZBUCIZCUCIZLZDUCIZEUCIZFUCIZLZLABCMMDEFMMNOBCAMMEFDMMNOZC
    ABMMFDEMMNOZABCDEFGPUBUBUGUEUFUDLUKUIUJUHLULUMQUBRUDUEUFSUHUIUJSBCAEFDGPTUA
    $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Permutation law for three-place congruence.  (Contributed by Scott Fenton,
     5-Oct-2013.) $)
  cgr3permute5 $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. <->
         <. C , <. B , A >. >. Cgr3 <. F , <. E , D >. >. ) ) $=
    ( cn wcel cee cfv w3a cop ccgr3 wbr cgr3permute3 biid 3anrot cgr3permute2
    wb syl3anb bitrd ) GHIZAGJKZIZBUDIZCUDIZLZDUDIZEUDIZFUDIZLZLABCMMDEFMMNOBCA
    MMEFDMMNOZCBAMMFEDMMNOZABCDEFGPUCUCUHUFUGUELULUJUKUILUMUNTUCQUEUFUGRUIUJUKR
    BCAEFDGSUAUB $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Transitivity law for three-place congruence.  (Contributed by Scott
     Fenton, 5-Oct-2013.) $)
  cgr3tr4 $p |- ( ( N e. NN /\
    ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
      ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) /\
      ( G e. ( EE ` N ) /\ H e. ( EE ` N ) /\ I e. ( EE ` N ) ) ) ) ->
    ( ( <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. /\
        <. A , <. B , C >. >. Cgr3 <. G , <. H , I >. >. ) ->
      <. D , <. E , F >. >. Cgr3 <. G , <. H , I >. >. ) ) $=
    ( wcel w3a wa cop ccgr wbr ccgr3 wi axcgrtr syl133anc cn cee cfv 3an6 simpl
    simpr11 simpr12 simpr21 simpr22 simpr31 simpr32 simpr13 3anim123d wb brcgr3
    simpr23 simpr33 syl5bir 3adant3r3 3adant3r2 anbi12d 3adant3r1 3imtr4d ) JUA
    KZAJUBUCZKZBVEKZCVEKZLZDVEKZEVEKZFVEKZLZGVEKZHVEKZIVEKZLZLZMZABNZDENZOPZACN
    ZDFNZOPZBCNZEFNZOPZLZVTGHNZOPZWCGINZOPZWFHINZOPZLZMZWAWJOPZWDWLOPZWGWNOPZLZ
    AWFNZDWGNZQPZXBGWNNZQPZMXCXEQPZWQWBWKMZWEWMMZWHWOMZLVSXAWBWKWEWMWHWOUDVSXHW
    RXIWSXJWTVSVDVFVGVJVKVNVOXHWRRVDVRUEZVFVGVHVMVQVDUFZVFVGVHVMVQVDUGZVJVKVLVI
    VQVDUHZVJVKVLVIVQVDUIZVNVOVPVIVMVDUJZVNVOVPVIVMVDUKZABDEGHJSTVSVDVFVHVJVLVN
    VPXIWSRXKXLVFVGVHVMVQVDULZXNVJVKVLVIVQVDUPZXPVNVOVPVIVMVDUQZACDFGIJSTVSVDVG
    VHVKVLVOVPXJWTRXKXMXRXOXSXQXTBCEFHIJSTUMURVSXDWIXFWPVDVIVMXDWIUNVQABCDEFJUO
    USVDVIVQXFWPUNVMABCGHIJUOUTVAVDVMVQXGXAUNVIDEFGHIJUOVBVC $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Commutativity law for three-place congruence.  (Contributed by Scott
     Fenton, 5-Oct-2013.) $)
  cgr3com $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. <->
         <. D , <. E , F >. >. Cgr3 <. A , <. B , C >. >. ) ) $=
    ( wcel w3a cop ccgr wbr ccgr3 wa wb 3simpa cgrcom syl3an 3simpb 3simpc cee
    cn cfv id 3anbi123d brcgr3 3com23 3bitr4d ) GUBHZAGUAUCZHZBUJHZCUJHZIZDUJHZ
    EUJHZFUJHZIZIZABJZDEJZKLZACJZDFJZKLZBCJZEFJZKLZIVAUTKLZVDVCKLZVGVFKLZIZAVFJ
    ZDVGJZMLVNVMMLZUSVBVIVEVJVHVKUIUIUNUKULNURUOUPNVBVIOUIUDZUKULUMPUOUPUQPABDE
    GQRUIUIUNUKUMNURUOUQNVEVJOVPUKULUMSUOUPUQSACDFGQRUIUIUNULUMNURUPUQNVHVKOVPU
    KULUMTUOUPUQTBCEFGQRUEABCDEFGUFUIURUNVOVLODEFABCGUFUGUH $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Identity law for three-place congruence.  (Contributed by Scott Fenton,
     6-Oct-2013.) $)
  cgr3rflx $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    <. A , <. B , C >. >. Cgr3 <. A , <. B , C >. >. ) $=
    ( cn wcel cee cfv w3a wa cop wbr ccgr cgrrflx 3adant3r3 3adant3r2 3adant3r1
    ccgr3 3jca wb brcgr3 3anidm23 mpbird ) DEFZADGHZFZBUEFZCUEFZIZJZABCKZKZULRL
    ZABKZUNMLZACKZUPMLZUKUKMLZIZUJUOUQURUDUFUGUOUHABDNOUDUFUHUQUGACDNPUDUGUHURU
    FBCDNQSUDUIUMUSTABCABCDUAUBUC $.
    $( [14-Oct-2013] $) $( [6-Oct-2013] $)

  ${
    $d A e f g $.  $d B e f g $.  $d C e f g $.  $d D e f g $.  $d F e f g $.
    $d N e f g $.
    $( A line segment can be divided at the same place as a congruent line
       segment is divided.  Theorem 4.5 of [Schwabhauser] p. 35.  (Contributed
       by Scott Fenton, 4-Oct-2013.) $)
    cgrxfr $p |- ( ( N e. NN /\
            ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
            ( D e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
            ( ( B Btwn <. A , C >. /\ <. A , C >. Cgr <. D , F >. ) ->
              E. e e. ( EE ` N ) ( e Btwn <. D , F >. /\
               <. A , <. B , C >. >. Cgr3 <. D , <. e , F >. >. ) ) ) $=
      ( vg vf wcel w3a wa cop cbtwn wbr ccgr wrex simpl1 wi ad2antrl cn cee cfv
      ccgr3 wne simpl3r simpl3l btwndiff syl3anc simpr simpl21 simpl22 axsegcon
      cv syl122anc adantr anass simprl simprr simpl23 df-3an anbi2i bitr4i wceq
      simplrr necomd simpr1 simpr2 simpr3 btwnexchand btwnexch3and cgrextendand
      simprrl simplll simprrr jca simplrl wb btwncom syl13anc simpllr cgrcomand
      mpbid 3jca segconeq syl133anc syld imp opeq2 breq2d breq1d anbi12d biimpa
      ex simpl btwnexch3 syl2ani adantl brcgr3 mpbird expr syl5 exp3acom23 impr
      mpd sylanb an32s rexlimdva reximdva ) GUAJZAGUBUCZJZBXKJZCXKJZKZDXKJZFXKJ
      ZLZKZBACMZNOZXTDFMZPOZLZEUNZYBNOZABCMZMDYEFMZMUDOZLZEXKQZXSYDLZDFHUNZMNOZ
      DYMUEZLZHXKQZYKYLXJXQXPYQXJXOXRYDRXPXQXJXOYDUFXPXQXJXOYDUGFDGHUHUIYLYPYKH
      XKXSYMXKJZYDYPYKSXSYRLZYDYPYKYSYDYPLZLZDYMYEMNOZDYEMZABMZPOZLZEXKQZYKYSUU
      GYTYSXJYRXPXLXMUUGXJXOXRYRRXSYRUJXPXQXJXOYRUGXLXMXNXJXRYRUKXLXMXNXJXRYRUL
      EYMDABGUMUOUPUUAUUFYJEXKYSYEXKJZYTUUFYJSZYSUUHLXSYRUUHLZLZYTUUIXSYRUUHUQU
      UKYTUUFYJUUKYTUUFLZLZYEYMIUNZMZNOZYEUUNMZYGPOZLZIXKQZYJUUKUUTUULUUKXJYRUU
      HXMXNUUTXJXOXRUUJRXSYRUUHURXSYRUUHUSXLXMXNXJXRUUJULXLXMXNXJXRUUJUTIYMYEBC
      GUMUOUPUUMUUSYJIXKUUKUUNXKJZUULUUSYJSZUUKUVALZXSYRUUHUVAKZLZUULUVBUVCXSUU
      JUVALZLUVEXSUUJUVAUQUVDUVFXSYRUUHUVAVAVBVCUVEUULUUSYJUVEUULUUSLZLZUUNFVDZ
      YJUVEUVGUVIUVEUVGYMDUEZDUUONOZDUUNMXTPOZLZDYMFMZNOZYBXTPOZLZKZUVIUVEUVGUV
      RUVHUVJUVMUVQUVHDYMUULYOUVEUUSYDYNYOUUFVETVFUVHUVKUVLUVEUVGYMDYEUUNGXJXOX
      RUVDRZXSYRUUHUVAVGZXPXQXJXOUVDUGZXSYRUUHUVAVHZXSYRUUHUVAVIZUULUUBUVEUUSYT
      UUBUUEURZTZUVEUULUUPUURVMZVJUVEUVGDYEUUNABCGUVSUWAUWBUWCXLXMXNXJXRUVDUKZX
      LXMXNXJXRUVDULZXLXMXNXJXRUVDUTZUVEUVGYMDYEUUNGUVSUVTUWAUWBUWCUWEUWFVKUULY
      AUVEUUSYAYCYPUUFVNTUULUUEUVEUUSYTUUBUUEUSTUVEUULUUPUURVOVLVPUVHUVOUVPUVHY
      NUVOUULYNUVEUUSYDYNYOUUFVQTUVEYNUVOVRZUVGUVEXJXPXQYRUWJUVSUWAXPXQXJXOUVDU
      FZUVTDFYMGVSVTUPWCUVEUVGACDFGUVSUWGUWIUWAUWKUULYCUVEUUSYAYCYPUUFWAZTWBVPW
      DWNUVEXJXPXLXNYRUVAXQUVRUVISUVSUWAUWGUWIUVTUWCUWKDACYMGUUNFWEWFWGWHUVEUUL
      UUSUVIYJSUVEUULLZUVIUUSYJUVIUUSLYEUVNNOZYHYGPOZLZUWMYJUVIUUSUWPUVIUUPUWNU
      URUWOUVIUUOUVNYENUUNFYMWIWJUVIUUQYHYGPUUNFYEWIWKWLWMUVEUULUWPYJUVEUULUWPL
      ZLZYFYIUVEUWQYFUULUVEUUBUWNYFUWPUWDUWNUWOWOUVEXJYRXPUUHXQUUBUWNLYFSUVSUVT
      UWAUWBUWKYMDYEFGWPUOWQWHUWRYIUUDUUCPOZYCYGYHPOZKZUWRUWSYCUWTUVEUWQDYEABGU
      VSUWAUWBUWGUWHUWQUUEUVEYTUUBUUEUWPVEWRWBUULYCUVEUWPUWLTUVEUWQYEFBCGUVSUWB
      UWKUWHUWIUVEUULUWNUWOVOWBWDUVEYIUXAVRZUWQUVEXJXLXMXNXPUUHXQUXBUVSUWGUWHUW
      IUWAUWBUWKABCDYEFGWSWFUPWTVPXAXBXCXDXEXAXFXGXHXEXAXFXGXIXEXAXGXHXEWN $.
      $( [14-Oct-2013] $) $( [4-Oct-2013] $)
  $}

  ${
    $d N e $.  $d A e $.  $d B e $.  $d C e $.  $d D e $.  $d E e $.  $d F e $.
    $( A condition for extending betweenness to a new set of points based on
       congruence with another set of points.  Theorem 4.6 of [Schwabhauser]
       p. 36.  (Contributed by Scott Fenton, 4-Oct-2013.) $)
    btwnxfr $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( ( B Btwn <. A , C >. /\
           <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. ) ->
         E Btwn <. D , F >. ) ) $=
      ( ve wcel w3a cop cbtwn wbr ccgr3 wa ccgr wi imp jca simpr cn cee cv wrex
      cfv brcgr3 simp2 syl6bi simp21 simp22 simp23 simp31 simp33 cgrxfr sylan2d
      simp1 syl132anc wceq simprrl simpl1 simpl31 simpl33 cgrrflx adantr simpl2
      syl3anc simpl3 3jca cgr3tr4 wb cgr3com syl113anc simpl32 syl133anc simpr1
      syl13anc simpr3 cgrcomlr syl122anc mpbid sylbid syld syl2ani brifs ifscgr
      ex cifs sylbird syl333anc cgrid2 eqbrtrrd expr an32s rexlimdva mpd ) GUAI
      ZAGUBUEZIZBWQIZCWQIZJZDWQIZEWQIZFWQIZJZJZBACKZLMZABCKZKZDEFKZKZNMZOZEDFKZ
      LMZXFXNOZHUCZXOLMZXJDXRFKZKZNMZOZHWQUDZXPXFXNYDXFXMXGXOPMZXHYDXFXMABKDEKZ
      PMZYEXIXKPMZJYEABCDEFGUFYGYEYHUGUHXFWPWRWSWTXBXDXHYEOYDQWPXAXEUPWPWRWSWTX
      EUIWPWRWSWTXEUJWPWRWSWTXEUKWPXAXBXCXDULWPXAXBXCXDUMABCDHFGUNUQUORXQYCXPHW
      QXFXRWQIZXNYCXPQXFYIOZXNYCXPYJXNYCOZOZXREXOLYJYKXREURZYJYKXRXRKXREKPMZYMY
      JYKXSXSOZXOXOPMZXTXTPMZOZDXRKZYFPMZFXRKZFEKZPMZOZJZYNYJYKUUEYLYOYRUUDYLXS
      XSYJXNXSYBUSZUUFSYJYRYKYJYPYQYJWPXBXDYPWPXAXEYIUTZXBXCXDWPXAYIVAZXBXCXDWP
      XAYIVBZDFGVCVFYJWPYIXDYQUUGXFYITZUUIXRFGVCVFSVDYJYKUUDXNYJXMYBUUDYCXHXMTX
      SYBTYJXMYBOZXLYANMZUUDYJWPXAXEXBYIXDJUUKUULQUUGWPXAXEYIVEWPXAXEYIVGZYJXBY
      IXDUUHUUJUUIVHABCDEFDXRFGVIVPYJUULYAXLNMZUUDYJWPXEXBYIXDUULUUNVJUUGUUMUUH
      UUJUUIDEFDXRFGVKVLYJUUNYTYPXTXKPMZJZUUDYJWPXBYIXDXBXCXDUUNUUPVJUUGUUHUUJU
      UIUUHXBXCXDWPXAYIVMZUUIDXRFDEFGUFVNYJUUPUUDYJUUPOZYTUUCYJYTYPUUOVOUURUUOU
      UCYJYTYPUUOVQYJUUOUUCVJZUUPYJWPYIXDXCXDUUSUUGUUJUUIUUQUUIXRFEFGVRVSVDVTSW
      FWAWAWBWCRVHWFYJWPXBYIXDYIXBYIXDXCUUEYNQUUGUUHUUJUUIUUJUUHUUJUUIUUQWPXBYI
      JXDYIXBJYIXDXCJJUUEYSUUAKYSUUBKWGMYNDXRFXRDXRFEGWDDXRFXRDXRFEGWEWHWIWBYJW
      PYIYIXCYNYMQUUGUUJUUJUUQXRXREGWJVPWBRUUFWKWLWMWNWOWF $.
      $( [14-Oct-2013] $) $( [4-Oct-2013] $)
  $}

  ${
    $d p q r n $.
    $( Colinearity is a relationship.  (Contributed by Scott Fenton,
       7-Nov-2013). $)
    colinrel $p |- Rel Colinear $=
      ( vp vn vq vr ccolin wrel cv cee cfv wcel w3a cop cbtwn wbr w3o wa copab2
      cn wrex ccnv relcnv df-colinear releqi mpbir ) EFAGZBGHIZJCGZUFJDGZUFJKUE
      UGUHLMNUGUHUELMNUHUEUGLMNOPBRSCDAQZTZFUIUAEUJBACDUBUCUD $.
      $( [19-Apr-2014] $) $( [7-Nov-2013] $)
  $}

  ${
    $d n p $.  $d n q $.  $d n r $.  $d P n $.  $d P p $.  $d p q $.  $d P q $.
    $d p r $.  $d P r $.  $d Q n $.  $d Q p $.  $d Q q $.  $d q r $.  $d Q r $.
    $d R n $.  $d R p $.  $d R q $.  $d R r $.

    $( Alternate colinearity binary relationship.  (Contributed by Scott
       Fenton, 7-Oct-2013). $)
    brcolinear2 $p |- ( ( Q e. V /\ R e. W ) ->
        ( P Colinear <. Q , R >. <-> E. n e. NN
          ( ( P e. ( EE ` n ) /\ Q e. ( EE ` n ) /\ R e. ( EE ` n ) ) /\
            ( P Btwn <. Q , R >. \/ Q Btwn <. R , P >. \/
              R Btwn <. P , Q >. ) ) ) ) $=
      ( vp vq vr wcel wa cvv cop ccolin wbr cv w3a cbtwn cn breq2d cee cfv wrex
      w3o wi colinrel brrelexi a1i elex 3ad2ant1 adantr rexlimivw wb ccnv df-br
      copab2 df-colinear eleq2i bitri opex opelcnvg mpan2 3ad2ant3 syl5bb eleq1
      wceq 3anbi2d opeq1 breq1 opeq2 3orbi123d anbi12d rexbidv 3anbi3d eloprabg
      3anbi1d bitrd 3expia pm5.21ndd ) BEJZCFJZKZALJZABCMZNOZADPUAUBZJZBWFJZCWF
      JZQZAWDROZBCAMZROZCABMZROZUDZKZDSUCZWEWCUEWBAWDNUFUGUHWRWCUEWBWQWCDSWJWCW
      PWGWHWCWIAWFUIUJUKULUHVTWAWCWEWRUMVTWAWCQZWEWDAMGPZWFJZHPZWFJZIPZWFJZQZWT
      XBXDMZROZXBXDWTMZROZXDWTXBMZROZUDZKZDSUCZHIGUPZJZWRWEAWDMZXPUNZJZWSXQWEXR
      NJXTAWDNUONXSXRDGHIUQURUSWCVTXTXQUMZWAWCWDLJYABCUTAWDLLXPVAVBVCVDXOXAWHXE
      QZWTBXDMZROZBXIROZXDWTBMZROZUDZKZDSUCXAWHWIQZWTWDROZBCWTMZROZCYFROZUDZKZD
      SUCWRHIGBCAEFLXBBVFZXNYIDSYQXFYBXMYHYQXCWHXAXEXBBWFVEVGYQXHYDXJYEXLYGYQXG
      YCWTRXBBXDVHTXBBXIRVIYQXKYFXDRXBBWTVJTVKVLVMXDCVFZYIYPDSYRYBYJYHYOYRXEWIX
      AWHXDCWFVEVNYRYDYKYEYMYGYNYRYCWDWTRXDCBVJTYRXIYLBRXDCWTVHTXDCYFRVIVKVLVMW
      TAVFZYPWQDSYSYJWJYOWPYSXAWGWHWIWTAWFVEVPYSYKWKYMWMYNWOWTAWDRVIYSYLWLBRWTA
      CVJTYSYFWNCRWTABVHTVKVLVMVOVQVRVS $.
      $( [19-Apr-2014] $) $( [7-Nov-2013] $)
  $}

  ${
    $d A a b c n $.  $d B a b c n $.  $d C a b c n $.  $d N a b c n $.
    $( The binary relationship form of the colinearity predicate.  (Contributed
       by Scott Fenton, 5-Oct-2013.) $)
    brcolinear $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
       ( A Colinear <. B , C >. <->
         ( A Btwn <. B , C >. \/ B Btwn <. C , A >. \/
           C Btwn <. A , B >. ) ) ) $=
      ( vn cn wcel cee cfv w3a wa cop ccolin wbr cv cbtwn w3o wrex wb eleq2d
      brcolinear2 3adant1 adantl simpr rexlimivw fveq2 3anbi123d anbi1d rcla4ev
      wceq expr impbid2 bitrd ) DFGZADHIZGZBUOGZCUOGZJZKZABCLZMNZAEOZHIZGZBVDGZ
      CVDGZJZAVAPNBCALPNCABLPNQZKZEFRZVIUSVBVKSZUNUQURVLUPABCEUOUOUAUBUCUTVKVIV
      JVIEFVHVIUDUEUNUSVIVKVJUSVIKEDFVCDUJZVHUSVIVMVEUPVFUQVGURVMVDUOAVCDHUFZTV
      MVDUOBVNTVMVDUOCVNTUGUHUIUKULUM $.
      $( [14-Oct-2013] $) $( [5-Oct-2013] $)
  $}

  ${
    $d a b $.  $d a c $.  $d a n $.  $d a x $.  $d b c $.  $d b n $.  $d b x $.
    $d c n $.  $d c x $.  $d n x $.
    $( The colinear predicate exists.  (Contributed by Scott Fenton,
       25-Oct-2013.) $)
    colinearex $p |- Colinear e. _V $=
      ( va vn vb vc vx ccolin cv cee cfv wcel cop cbtwn wbr wa cn wrex cxp xpex
      wex opelxpi w3a w3o copab2 ccnv cvv df-colinear ciun nnex fvex iunex wceq
      cab 3adant1 simp1 syl2anc adantr reximi eliun sylibr eleq1 biimpar sylan2
      df-oprab exlimiv exlimivv abssi eqsstri ssexi cnvex eqeltri ) FAGZBGZHIZJ
      ZCGZVMJZDGZVMJZUAZVKVOVQKZLMVOVQVKKLMVQVKVOKLMUBZNZBOPZCDAUCZUDUEBACDUFWD
      WDBOVMVMQZVMQZUGZBOWFUHWEVMVMVMVLHUIZWHRWHRUJWDEGZVTVKKZUKZWCNZASZDSCSZEU
      LWGWCCDAEVCWNEWGWMWIWGJZCDWLWOAWCWKWJWGJZWOWCWJWFJZBOPWPWBWQBOVSWQWAVSVTW
      EJZVNWQVPVRWRVNVOVQVMVMTUMVNVPVRUNVTVKWEVMTUOUPUQBWJOWFURUSWKWOWPWIWJWGUT
      VAVBVDVEVFVGVHVIVJ $.
      $( [19-Apr-2014] $) $( [25-Oct-2013] $)
  $}

  ${
    $d A a $.  $d a b $.  $d A b $.  $d a c $.  $d A c $.  $d a n $.  $d A n $.
    $d B a $.  $d B b $.  $d b c $.  $d B c $.  $d b n $.  $d B n $.  $d C a $.
    $d C b $.  $d C c $.  $d c n $.  $d C n $.  $d N n $.  $d V n $.  $d W n $.
    $( If ` A ` is colinear with ` B ` and ` C ` , then ` A ` is in the same
       space as ` B ` .  (Contributed by Scott Fenton, 25-Oct-2013.) $)
    colineardim1 $p |- (
       ( N e. NN /\ ( A e. V /\ B e. ( EE ` N ) /\ C e. W ) ) ->
       ( A Colinear <. B , C >. ->
       A e. ( EE ` N ) ) ) $=
      ( va vn vb vc cop wbr cv wcel w3a cbtwn w3o wa cn breq2d ccolin wrex ccnv
      cee cfv copab2 df-colinear breqi wb simpr1 opex brcnvg sylancl df-br wceq
      eleq1 3anbi2d opeq1 breq1 opeq2 3orbi123d anbi12d rexbidv 3anbi3d 3anbi1d
      eloprabg 3comr adantl simpl simp2 anim2i axdimuniq adantrrl simprrl fveq2
      cvv 3simpa eleq2d syl5ibrcom mpd syl2an exp32 syl7 rexlimdv sylbid syl5bi
      ) ABCKZUALAWGGMZHMZUDUEZNZIMZWJNZJMZWJNZOZWHWLWNKZPLZWLWNWHKZPLZWNWHWLKZP
      LZQZRZHSUBZIJGUFZUCZLZDSNZAENZBDUDUEZNZCFNZOZRZAXKNZAWGUAXGHGIJUGUHXOXHWG
      AXFLZXPXOXJWGVPNXHXQUIXIXJXLXMUJBCUKAWGEVPXFULUMXQWGAKXFNZXOXPWGAXFUNXOXR
      AWJNZBWJNZCWJNZOZAWGPLZBCAKZPLZCABKZPLZQZRZHSUBZXPXNXRYJUIZXIXLXMXJYKXEWK
      XTWOOZWHBWNKZPLZBWSPLZWNWHBKZPLZQZRZHSUBWKXTYAOZWHWGPLZBCWHKZPLZCYPPLZQZR
      ZHSUBYJIJGBCAXKFEWLBUOZXDYSHSUUGWPYLXCYRUUGWMXTWKWOWLBWJUPUQUUGWRYNWTYOXB
      YQUUGWQYMWHPWLBWNURTWLBWSPUSUUGXAYPWNPWLBWHUTTVAVBVCWNCUOZYSUUFHSUUHYLYTY
      RUUEUUHWOYAWKXTWNCWJUPVDUUHYNUUAYOUUCYQUUDUUHYMWGWHPWNCBUTTUUHWSUUBBPWNCW
      HURTWNCYPPUSVAVBVCWHAUOZUUFYIHSUUIYTYBUUEYHUUIWKXSXTYAWHAWJUPVEUUIUUAYCUU
      CYEUUDYGWHAWGPUSUUIUUBYDBPWHACUTTUUIYPYFCPWHABURTVAVBVCVFVGVHXOYIXPHSYIYB
      XOWISNZXPYBYHVIXOUUJYBXPXOXIXLRZUUJXSXTRZRZXPUUJYBRXNXLXIXJXLXMVJVKYBUULU
      UJXSXTYAVQVKUUKUUMRZDWIUOZXPUUKUUJXTUUOXSBWIDVLVMUUNXPUUOXSUUKUUJXSXTVNUU
      OXKWJADWIUDVOVRVSVTWAWBWCWDWEWFWEWF $.
      $( [19-Apr-2014] $) $( [25-Oct-2013] $)
  $}

  $( Permutation law for colinearity.  Part of theorem 4.11 of [Schwabhauser]
     p. 36.  (Contributed by Scott Fenton, 5-Oct-2013.) $)
  colinearperm1 $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( A Colinear <. B , C >. <-> A Colinear <. C , B >. ) ) $=
    ( cn wcel cee cfv w3a wa cop cbtwn wbr w3o ccolin btwncom wb 3anrot sylan2b
    brcolinear sylan2br 3orbi123d 3orcomb syl6bb 3ancomb 3bitr4d ) DEFZADGHZFZB
    UHFZCUHFZIZJZABCKZLMZBCAKLMZCABKLMZNZACBKZLMZCBAKLMZBACKLMZNZAUNOMAUSOMZUMU
    RUTVBVANVCUMUOUTUPVBUQVAABCDPULUGUJUKUIIUPVBQUIUJUKRBCADPSULUGUKUIUJIUQVAQU
    KUIUJRCABDPUAUBUTVBVAUCUDABCDTULUGUIUKUJIVDVCQUIUJUKUEACBDTSUF $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Permutation law for colinearity.  Part of theorem 4.11 of [Schwabhauser]
     p. 36.  (Contributed by Scott Fenton, 5-Oct-2013.) $)
  colinearperm3 $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( A Colinear <. B , C >. <-> B Colinear <. C , A >. ) ) $=
    ( cn wcel cee cfv w3a cop cbtwn wbr w3o ccolin 3orrot a1i brcolinear 3anrot
    wa wb sylan2b 3bitr4d ) DEFZADGHZFZBUDFZCUDFZIZSZABCJZKLZBCAJZKLZCABJKLZMZU
    MUNUKMZAUJNLBULNLZUOUPTUIUKUMUNOPABCDQUHUCUFUGUEIUQUPTUEUFUGRBCADQUAUB $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Permutation law for colinearity.  Part of theorem 4.11 of [Schwabhauser]
     p. 36.  (Contributed by Scott Fenton, 5-Oct-2013.) $)
  colinearperm2 $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( A Colinear <. B , C >. <-> B Colinear <. A , C >. ) ) $=
    ( cn wcel cee cfv w3a wa cop ccolin wbr colinearperm3 colinearperm1 sylan2b
    wb 3anrot bitrd ) DEFZADGHZFZBUAFZCUAFZIZJABCKLMBCAKLMZBACKLMZABCDNUETUCUDU
    BIUFUGQUBUCUDRBCADOPS $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Permutation law for colinearity.  Part of theorem 4.11 of [Schwabhauser]
     p. 36.  (Contributed by Scott Fenton, 5-Oct-2013.) $)
  colinearperm4 $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( A Colinear <. B , C >. <-> C Colinear <. A , B >. ) ) $=
    ( cn wcel cee cfv w3a wa cop ccolin wbr colinearperm3 3anrot sylan2b bitrd
    wb ) DEFZADGHZFZBTFZCTFZIZJABCKLMBCAKLMZCABKLMZABCDNUDSUBUCUAIUEUFRUAUBUCOB
    CADNPQ $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Permutation law for colinearity.  Part of theorem 4.11 of [Schwabhauser]
     p. 36.  (Contributed by Scott Fenton, 5-Oct-2013.) $)
  colinearperm5 $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( A Colinear <. B , C >. <-> C Colinear <. B , A >. ) ) $=
    ( cn wcel cee cfv w3a wa cop ccolin colinearperm4 wb colinearperm1 sylan2br
    wbr 3anrot bitrd ) DEFZADGHZFZBUAFZCUAFZIZJABCKLQCABKLQZCBAKLQZABCDMUETUDUB
    UCIUFUGNUDUBUCRCABDOPS $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Trivial case of colinearity.  Theorem 4.12 of [Schwabhauser] p. 37.
     (Contributed by Scott Fenton, 5-Oct-2013.) $)
  colineartriv1 $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
    A Colinear <. A , B >. ) $=
    ( cn wcel cee cfv w3a cop ccolin wbr cbtwn w3o btwntriv1 3mix1d simp1 simp2
    wb simp3 brcolinear syl13anc mpbird ) CDEZACFGZEZBUDEZHZAABIZJKZAUHLKZABAIL
    KZBAAILKZMZUGUJUKULABCNOUGUCUEUEUFUIUMRUCUEUFPUCUEUFQZUNUCUEUFSAABCTUAUB $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Trivial case of colinearity.  (Contributed by Scott Fenton,
     18-Oct-2013.) $)
  colineartriv2 $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
    A Colinear <. B , B >. ) $=
    ( cn wcel cee cfv w3a cop ccolin wbr cbtwn btwntriv1 3mix2d 3com23 wb simp1
    w3o simp2 simp3 brcolinear syl13anc mpbird ) CDEZACFGZEZBUEEZHZABBIZJKZAUIL
    KZBBAILKZBABILKZRZUDUGUFUNUDUGUFHULUKUMBACMNOUHUDUFUGUGUJUNPUDUFUGQUDUFUGSU
    DUFUGTZUOABBCUAUBUC $.
    $( [19-Apr-2014] $) $( [18-Oct-2013] $)

  $( Betweenness implies colinearity.  (Contributed by Scott Fenton,
     7-Oct-2013.) $)
  btwncolinear1 $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( C Btwn <. A , B >. -> A Colinear <. B , C >. ) ) $=
    ( cop cbtwn wbr ccolin cn wcel cee cfv w3a wa w3o 3mix3 brcolinear syl5ibr
    ) CABEFGZABCEZHGDIJADKLZJBUAJCUAJMNATFGZBCAEFGZSOSUBUCPABCDQR $.
    $( [14-Oct-2013] $) $( [7-Oct-2013] $)


  $( Betweenness implies colinearity.  (Contributed by Scott Fenton,
     15-Oct-2013.) $)
  btwncolinear2 $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( C Btwn <. A , B >. -> A Colinear <. C , B >. ) ) $=
    ( cn cee cfv w3a wa cop cbtwn wbr ccolin btwncolinear1 colinearperm1 sylibd
    wcel ) DEQADFGZQBRQCRQHICABJKLABCJMLACBJMLABCDNABCDOP $.
    $( [19-Apr-2014] $) $( [15-Oct-2013] $)

  $( Betweenness implies colinearity.  (Contributed by Scott Fenton,
     15-Oct-2013.) $)
  btwncolinear3 $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( C Btwn <. A , B >. -> B Colinear <. A , C >. ) ) $=
    ( cn cee cfv w3a wa cop cbtwn wbr ccolin btwncolinear1 colinearperm2 sylibd
    wcel ) DEQADFGZQBRQCRQHICABJKLABCJMLBACJMLABCDNABCDOP $.
    $( [19-Apr-2014] $) $( [15-Oct-2013] $)

  $( Betweenness implies colinearity.  (Contributed by Scott Fenton,
     15-Oct-2013.) $)
  btwncolinear4 $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( C Btwn <. A , B >. -> B Colinear <. C , A >. ) ) $=
    ( cn cee cfv w3a wa cop cbtwn wbr ccolin btwncolinear1 colinearperm3 sylibd
    wcel ) DEQADFGZQBRQCRQHICABJKLABCJMLBCAJMLABCDNABCDOP $.
    $( [19-Apr-2014] $) $( [15-Oct-2013] $)

  $( Betweenness implies colinearity.  (Contributed by Scott Fenton,
     15-Oct-2013.) $)
  btwncolinear5 $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( C Btwn <. A , B >. -> C Colinear <. A , B >. ) ) $=
    ( cn cee cfv w3a wa cop cbtwn wbr ccolin btwncolinear1 colinearperm4 sylibd
    wcel ) DEQADFGZQBRQCRQHICABJZKLABCJMLCSMLABCDNABCDOP $.
    $( [19-Apr-2014] $) $( [15-Oct-2013] $)

  $( Betweenness implies colinearity.  (Contributed by Scott Fenton,
     15-Oct-2013.) $)
  btwncolinear6 $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( C Btwn <. A , B >. -> C Colinear <. B , A >. ) ) $=
    ( cn cee cfv w3a wa cop cbtwn wbr ccolin btwncolinear1 colinearperm5 sylibd
    wcel ) DEQADFGZQBRQCRQHICABJKLABCJMLCBAJMLABCDNABCDOP $.
    $( [19-Apr-2014] $) $( [15-Oct-2013] $)


  $( Transfer law for colinearity.  Theorem 4.13 of [Schwabhauser] p. 37.
     (Contributed by Scott Fenton, 5-Oct-2013.) $)
  colinearxfr $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( ( B Colinear <. A , C >. /\
           <. A , <. B , C >. >. Cgr3 <. D , <. E , F >. >. ) ->
         E Colinear <. D , F >. ) ) $=
    ( wcel w3a cop ccolin wbr ccgr3 wi wa cbtwn btwnxfr exp3acom23 imp 3anrot
    cn cee cfv cgr3permute4 biid syl3anbr sylbid cgr3permute3 syl3anb 3orim123d
    w3o wb simp22 simp21 simp23 brcolinear syl13anc adantr simp32 simp31 simp33
    simp1 3imtr4d ex com23 imp3a ) GUAHZAGUBUCZHZBVHHZCVHHZIZDVHHZEVHHZFVHHZIZI
    ZBACJZKLZABCJJDEFJJMLZEDFJZKLZVQVTVSWBVQVTVSWBNVQVTOZBVRPLZACBJPLZCBAJPLZUK
    ZEWAPLZDFEJPLZFEDJPLZUKZVSWBWCWDWHWEWIWFWJVQVTWDWHNVQWDVTWHABCDEFGQRSVQVTWE
    WINZVQVTCABJJFDEJJMLZWLABCDEFGUDVQWEWMWIVGVGVLVKVIVJIVPVOVMVNIWEWMOWINVGUEZ
    VKVIVJTVOVMVNTCABFDEGQUFRUGSVQVTWFWJNZVQVTBCAJJEFDJJMLZWOABCDEFGUHVQWFWPWJV
    GVGVLVJVKVIIVPVNVOVMIWFWPOWJNWNVIVJVKTVMVNVOTBCAEFDGQUIRUGSUJVQVSWGULZVTVQV
    GVJVIVKWQVGVLVPVBZVGVIVJVKVPUMVGVIVJVKVPUNVGVIVJVKVPUOBACGUPUQURVQWBWKULZVT
    VQVGVNVMVOWSWRVGVLVMVNVOUSVGVLVMVNVOUTVGVLVMVNVOVAEDFGUPUQURVCVDVEVF $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  ${
    $d N f $.  $d A f $.  $d B f $.  $d C f $.  $d D f $.  $d E f $.
    $( Extend a line with a missing point.  Theorem 4.14 of [Schwabhauser]
       p. 37.  (Contributed by Scott Fenton, 6-Oct-2013.) $)
    lineext $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) ->
       ( ( A Colinear <. B , C >. /\ <. A , B >. Cgr <. D , E >. ) ->
         E. f e. ( EE ` N )
            <. A , <. B , C >. >. Cgr3 <. D , <. E , f >. >. ) ) $=
      ( wcel w3a wa cop wbr ccgr cbtwn wrex wb wi 3jca adantr anbi2d cn cee cfv
      ccolin w3o ccgr3 brcolinear 3adant3 anbi1d simp1 simp3r simp3l jca simp21
      cv simp23 axsegcon syl simprlr simprrr an4 simpl1 simpl21 simpl22 simpl3l
      simpl3r cgrcomlr syl122anc simpl23 cgrextend syl133anc sylbid syl5bi expr
      simpr imp cgrcom simpl2 brcgr3 syl113anc 3imtr4d an32s reximdva mpd exp32
      3ancoma btwncom simp3 simp22 syl112anc simpll simplr adantl sylcom syl5bb
      sylan2b ex expdimp sylbird cgrxfr cgr3permute1 biimprd adantld syld exp3a
      syl131anc 3jaod imp3a ) GUAHZAGUBUCZHZBXJHZCXJHZIZDXJHZFXJHZJZIZABCKZUDLZ
      ABKZDFKZMLZJAXSNLZBCAKNLZCYANLZUEZYCJAXSKDFEUOZKZKUFLZEXJOZXRXTYGYCXIXNXT
      YGPXQABCGUGUHUIXRYGYCYKXRYDYCYKQZYEYFXRYDYCYKXRYDYCJZJZDYINLZDYHKZACKZMLZ
      JZEXJOZYKYNXIXPXOJZXKXMJZIZYTXRUUCYMXRXIUUAUUBXIXNXQUJZXRXPXOXIXNXOXPUKXI
      XNXOXPULUMXRXKXMXIXKXLXMXQUNZXIXKXLXMXQUPZUMRSEFDACGUQURYNYSYJEXJXRYHXJHZ
      YMYSYJQXRUUGJZYMJYOYQYPMLZJZYCUUIXSYIMLZIZYSYJUUHYMUUJUULUUHYMUUJJZJYCUUI
      UUKUUHYDYCUUJUSUUHYMYOUUIUTUUHUUMUUKUUMYDYOJZYCUUIJZJZUUHUUKYDYCYOUUIVAUU
      HUUPUUNBAKFDKMLZUUIJZJZUUKUUHUUOUURUUNUUHYCUUQUUIUUHXIXKXLXOXPYCUUQPXIXNX
      QUUGVBZXKXLXMXIXQUUGVCZXKXLXMXIXQUUGVDZXOXPXIXNUUGVEZXOXPXIXNUUGVFZABDFGV
      GVHUITUUHXIXLXKXMXPXOUUGUUSUUKQUUTUVBUVAXKXLXMXIXQUUGVIZUVDUVCXRUUGVOZBAC
      FDYHGVJVKVLVMVPRVNUUHYSUUJPYMUUHYRUUIYOUUHXIXOUUGXKXMYRUUIPUUTUVCUVFUVAUV
      EDYHACGVQVHTSUUHYJUULPZYMUUHXIXNXOXPUUGUVGUUTXIXNXQUUGVRZUVCUVDUVFABCDFYH
      GVSVTZSWAWBWCWDWEXRYEBYQNLZYLXIXNUVJYEPZXQXNXIXLXKXMIUVKXKXLXMWFBACGWGWPU
      HXRUVJYCYKXRUVJYCJZJZFYPNLZYIXSMLZJZEXJOZYKXRUVQUVLXRXIXQXLXMUVQUUDXIXNXQ
      WHZXIXKXLXMXQWIZUUFEDFBCGUQWJSUVMUVPYJEXJXRUUGUVLUVPYJQUUHUVLUVPYJUUHUVJU
      VNJZYCUUKJZJZUULUVLUVPJZYJUUHUWBUUIUULUUHXIXNXOXPUUGUWBUUIQUUTUVHUVCUVDUV
      FABCDFYHGVJVTUWAUUIUULQUVTUWAUUIUULUWAUUIJYCUUIUUKYCUUKUUIWKUWAUUIVOYCUUK
      UUIWLRWQWMWNUWCUVTYCUVOJZJUUHUWBUVJYCUVNUVOVAUUHUWDUWAUVTUUHUVOUUKYCUUHXI
      XPUUGXLXMUVOUUKPUUTUVDUVFUVBUVEFYHBCGVQVHTTWOUVIWAWRWBWCWDWEWSXRYFYCYKXRY
      FYCJZYHYBNLZACBKKDYHFKKUFLZJZEXJOZYKXRXIXKXMXLXQUWEUWIQUUDUUEUUFUVSUVRACB
      DEFGWTXFXRUWHYJEXJUUHUWGYJUWFUUHYJUWGUUHXIXNXOXPUUGYJUWGPUUTUVHUVCUVDUVFA
      BCDFYHGXAVTXBXCWCXDXEXGXHVL $.
      $( [14-Oct-2013] $) $( [6-Oct-2013] $)
  $}

  $( Change some conditions for outer five segment predicate.  (Contributed by
     Scott Fenton, 6-Oct-2013.) $)
  brofs2 $p |- ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
        ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
    ( <. <. A , B >. , <. C , D >. >. OuterFiveSeg
      <. <. E , F >. , <. G , H >. >. <->
      ( B Btwn <. A , C >. /\
        <. A , <. B , C >. >. Cgr3 <. E , <. F , G >. >. /\
        ( <. A , D >. Cgr <. E , H >. /\
          <. B , D >. Cgr <. F , H >. ) ) ) ) $=
    ( wcel w3a cop wbr cbtwn wa ccgr simpr1 syl133anc imp 3jca cn cee cfv ccgr3
    cofs brofs simpr1l simpr2l simpr2 jca ex simp11 simp12 simp13 simp21 simp23
    wi simp31 simp32 cgrextend syld simpr2r brcgr3 sylibrd simpr3 3simpa 3simpb
    wb btwnxfr syl5 syl6bi 3ad2antr2 impbida bitrd ) IUAJZAIUBUCZJZBVPJZKZCVPJZ
    DVPJZEVPJZKZFVPJZGVPJZHVPJZKZKZABLZCDLLEFLZGHLLUEMBACLZNMZFEGLZNMZOZWIWJPMZ
    BCLZFGLZPMZOZADLEHLPMBDLFHLPMOZKZWLAWQLEWRLUDMZXAKZABCDEFGHIUFWHXBXDWHXBOZW
    LXCXAWLWNWTXAWHUGWHXBXCWHXBWPWKWMPMZWSKZXCWHXBXGXEWPXFWSWPWSWOXAWHUHWHXBXFW
    HXBWOWTOZXFWHXBXHXEWOWTWHWOWTXAQWHWOWTXAUIUJUKWHVOVQVRVTWBWDWEXHXFUQVOVQVRW
    CWGULZVOVQVRWCWGUMZVOVQVRWCWGUNZVSVTWAWBWGUOZVSVTWAWBWGUPZVSWCWDWEWFURZVSWC
    WDWEWFUSZABCEFGIUTRVASWPWSWOXAWHVBTUKWHVOVQVRVTWBWDWEXCXGVHXIXJXKXLXMXNXOAB
    CEFGIVCRZVDSWHWOWTXAVETWHXDOZWOWTXAXQWLWNWHWLXCXAQWHXDWNXDWLXCOZWHWNWLXCXAV
    FWHVOVQVRVTWBWDWEXRWNUQXIXJXKXLXMXNXOABCEFGIVIRVJSUJWHWLXCWTXAWHXCWTWHXCXGW
    TXPWPXFWSVGVKSVLWHWLXCXAVETVMVN $.
    $( [14-Oct-2013] $) $( [6-Oct-2013] $)

  $( Change some conditions for inner five segment predicate.  (Contributed by
     Scott Fenton, 6-Oct-2013.) $)
  brifs2 $p |- ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
        ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
    ( <. <. A , B >. , <. C , D >. >. InnerFiveSeg
      <. <. E , F >. , <. G , H >. >. <->
      ( B Btwn <. A , C >. /\
        <. A , <. B , C >. >. Cgr3 <. E , <. F , G >. >. /\
        ( <. A , D >. Cgr <. E , H >. /\
          <. C , D >. Cgr <. G , H >. ) ) ) ) $=
    ( wcel w3a cop wbr cbtwn wa ccgr 3simpa syl133anc imp 3jca cn cee cfv ccgr3
    cifs brifs simpr1l wi simp11 simp12 simp13 simp21 simp23 simp31 simp32 syl5
    cgrsub simpr2l simpr2r ex wb brcgr3 sylibrd simpr3 simpr1 jca 3simpc syl6bi
    btwnxfr 3ad2antr2 impbida bitrd ) IUAJZAIUBUCZJZBVNJZKZCVNJZDVNJZEVNJZKZFVN
    JZGVNJZHVNJZKZKZABLZCDLZLEFLZGHLZLUEMBACLZNMZFEGLZNMZOZWKWMPMZBCLZFGLZPMZOZ
    ADLEHLPMWHWJPMOZKZWLAWQLEWRLUDMZXAKZABCDEFGHIUFWFXBXDWFXBOZWLXCXAWLWNWTXAWF
    UGWFXBXCWFXBWGWIPMZWPWSKZXCWFXBXGXEXFWPWSWFXBXFXBWOWTOZWFXFWOWTXAQWFVMVOVPV
    RVTWBWCXHXFUHVMVOVPWAWEUIZVMVOVPWAWEUJZVMVOVPWAWEUKZVQVRVSVTWEULZVQVRVSVTWE
    UMZVQWAWBWCWDUNZVQWAWBWCWDUOZABCEFGIUQRUPSWPWSWOXAWFURWPWSWOXAWFUSTUTWFVMVO
    VPVRVTWBWCXCXGVAXIXJXKXLXMXNXOABCEFGIVBRZVCSWFWOWTXAVDTWFXDOZWOWTXAXQWLWNWF
    WLXCXAVEWFXDWNXDWLXCOZWFWNWLXCXAQWFVMVOVPVRVTWBWCXRWNUHXIXJXKXLXMXNXOABCEFG
    IVIRUPSVFWFWLXCWTXAWFXCWTWFXCXGWTXPXFWPWSVGVHSVJWFWLXCXAVDTVKVL $.
    $( [14-Oct-2013] $) $( [6-Oct-2013] $)

  ${
    $d N a b c d e f g h p q n $.  $d A a b c d e f g h p q n $.
    $d B a b c d e f g h p q n $.  $d C a b c d e f g h p q n $.
    $d D a b c d e f g h p q n $.  $d E a b c d e f g h p q n $.
    $d F a b c d e f g h p q n $.  $d G a b c d e f g h p q n $.
    $d H a b c d e f g h p q n $.
    $( Binary relationship form of the general five segment predicate.
       (Contributed by Scott Fenton, 5-Oct-2013.) $)
    brfs $p |- ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
        ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
    ( <. <. A , B >. , <. C , D >. >. FiveSeg <. <. E , F >. , <. G , H >. >.
      <->
      ( A Colinear <. B , C >. /\
        <. A , <. B , C >. >. Cgr3 <. E , <. F , G >. >. /\
        ( <. A , D >. Cgr <. E , H >. /\ <. B , D >. Cgr <. F , H >. ) ) ) ) $=
      ( cv cop ccolin wbr ccgr3 ccgr wa w3a wceq opeq1 breq2d va vb vc ve vf vg
      vd vh vn vq vp cee cfv cfs cn breq1 breq1d anbi1d 3anbi123d opeq2d anbi2d
      opeq2 3anbi12d anbi12d 3anbi3d 3anbi23d 3anbi2d fveq2 df-fs br8 ) UAJZUBJ
      ZUCJZKZLMZVKVNKZUDJZUEJZUFJZKZKZNMZVKUGJZKZVQUHJZKZOMZVLWCKZVRWEKZOMZPZQA
      VNLMZAVNKZWANMZAWCKZWFOMZWJPZQABVMKZLMZAWRKZWANMZWPBWCKZWIOMZPZQABCKZLMZA
      XEKZWANMZXDQXFXHADKZWFOMZBDKZWIOMZPZQXFXGEVTKZNMZXIEWEKZOMZXLPZQXFXGEFVSK
      ZKZNMZXQXKFWEKZOMZPZQXFXGEFGKZKZNMZYDQXFYGXIEHKZOMZXKFHKZOMZPZQUIABCDUIJZ
      ULUMIULUMUNUOUDUEUFUHEFGHIUJUKUAUBUCUGVKARZVOWLWBWNWKWQVKAVNLUPYNVPWMWANV
      KAVNSUQYNWGWPWJYNWDWOWFOVKAWCSUQURUSVLBRZWLWSWNXAWQXDYOVNWRALVLBVMSZTYOWM
      WTWANYOVNWRAYPUTUQYOWJXCWPYOWHXBWIOVLBWCSUQVAUSVMCRZWSXFXAXHXDYQWRXEALVMC
      BVBZTYQWTXGWANYQWRXEAYRUTUQVCWCDRZXDXMXFXHYSWPXJXCXLYSWOXIWFOWCDAVBUQYSXB
      XKWIOWCDBVBUQVDVEVQERZXHXOXMXRXFYTWAXNXGNVQEVTSTYTXJXQXLYTWFXPXIOVQEWESTU
      RVFVRFRZXOYAXRYDXFUUAXNXTXGNUUAVTXSEVRFVSSUTTUUAXLYCXQUUAWIYBXKOVRFWESTVA
      VFVSGRZYAYGXFYDUUBXTYFXGNUUBXSYEEVSGFVBUTTVGWEHRZYDYLXFYGUUCXQYIYCYKUUCXP
      YHXIOWEHEVBTUUCYBYJXKOWEHFVBTVDVEYMIULVHUDUEUFUHUIUJUKUAUBUCUGVIVJ $.
      $( [14-Oct-2013] $) $( [5-Oct-2013] $)
  $}

  $( Congruence law for the general five segment configuration.  Theorem 4.16
     of [Schwabhauser] p. 37.  (Contributed by Scott Fenton, 5-Oct-3013). $)
  fscgr $p |- ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
      ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
      ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
   ( ( <. <. A , B >. , <. C , D >. >. FiveSeg <. <. E , F >. , <. G , H >. >.
       /\ A =/= B ) -> <. C , D >. Cgr <. G , H >. ) ) $=
    ( wcel w3a cop wbr wa ccgr3 wi cbtwn wb syl333anc sylbid cn cee cfv cfs wne
    ccolin ccgr brfs anbi1d w3o simp11 simp12 simp13 simp21 brcolinear syl13anc
    cofs simp23 simp31 simp32 cgr3permute2 syl133anc a1i 3anbi23d simp22 simp33
    ancom brofs2 necom anbi12d 5segofs exp3a 3expd btwncom 3anbi1d cgr3permute1
    bitr4d cifs 3anbi2d brifs2 ifscgr a1dd 3jaod 3impd imp3a ) IUAJZAIUBUCZJZBW
    GJZKZCWGJZDWGJZEWGJZKZFWGJZGWGJZHWGJZKZKZABLZCDLZLZEFLGHLZLZUDMZABUEZNABCLZ
    UFMZAXGLEFGLLOMZADLEHLUGMZBDLZFHLZUGMZNZKZXFNXAXCUGMZWSXEXOXFABCDEFGHIUHUIW
    SXOXFXPWSXHXIXNXFXPPZWSXHAXGQMZBCALQMZCWTQMZUJZXIXNXQPPZWSWFWHWIWKXHYARWFWH
    WIWNWRUKZWFWHWIWNWRULZWFWHWIWNWRUMZWJWKWLWMWRUNZABCIUOUPWSXRYBXSXTWSXRXIXNX
    QWSXRXIXNKZXFXPWSYGXFNBALXALFELXCLUQMZBAUEZNZXPWSYGYHXFYIWSYGXRBACLZLFEGLZL
    OMZXMXJNZKZYHWSXIYMXNYNXRWSWFWHWIWKWMWOWPXIYMRYCYDYEYFWJWKWLWMWRURZWJWNWOWP
    WQUSZWJWNWOWPWQUTZABCEFGIVAVBXNYNRWSXJXMVGVCVDWSWFWIWHWKWLWOWMWPWQYHYORYCYE
    YDYFWJWKWLWMWRVEZYQYPYRWJWNWOWPWQVFZBACDFEGHIVHSVQXFYIRWSABVIVCVJWSWFWIWHWK
    WLWOWMWPWQYJXPPYCYEYDYFYSYQYPYRYTBACDFEGHIVKSTVLVMWSXSXIXNXQWSXSXIXNKZXFXPW
    SUUAXFNXBXDUQMZXFNXPWSUUAUUBXFWSUUABYKQMZXIXNKUUBWSXSUUCXIXNWSWFWIWKWHXSUUC
    RYCYEYFYDBCAIVNUPVOABCDEFGHIVHVQUIABCDEFGHIVKTVLVMWSXTXIXNXQWSXTXIXNKZXPXFW
    SUUDYKXKLYLXLLVRMZXPWSUUDXTACBLLEGFLLOMZXNKZUUEWSXIUUFXTXNWSWFWHWIWKWMWOWPX
    IUUFRYCYDYEYFYPYQYRABCEFGIVPVBVSWSWFWHWKWIWLWMWPWOWQUUEUUGRYCYDYFYEYSYPYRYQ
    YTACBDEGFHIVTSVQWSWFWHWKWIWLWMWPWOWQUUEXPPYCYDYFYEYSYPYRYQYTACBDEGFHIWASTWB
    VMWCTWDWET $.
    $( [14-Oct-2013] $) $( [5-Oct-2013] $)

  $( Congruence rule for lines.  Theorem 4.17 of [Schwabhauser] p. 37.
     (Contributed by Scott Fenton, 6-Oct-2013.) $)
  linecgr $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
    ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) ) ) ->
    ( ( ( A =/= B /\ A Colinear <. B , C >. ) /\
        ( <. A , P >. Cgr <. A , Q >. /\ <. B , P >. Cgr <. B , Q >. ) ) ->
      <. C , P >. Cgr <. C , Q >. ) ) $=
    ( cn wcel cee cfv w3a wa wne cop ccolin wbr ccgr ccgr3 simprlr cgr3rflx jca
    3adant3 adantr simprr 3jca simprll ex wi simp21 simp22 simp23 simp3l simp3r
    simp1 cfs brfs anbi1d fscgr sylbird syl333anc syld ) FGHZAFIJZHZBVCHZCVCHZK
    ZDVCHZEVCHZLZKZABMZABCNZOPZLZADNAENQPBDNBENQPLZLZVNAVMNZVRRPZVPKZVLLZCDNZCE
    NZQPZVKVQWAVKVQLZVTVLWEVNVSVPVKVLVNVPSVKVSVQVBVGVSVJABCFTUBUCVKVOVPUDUEVKVL
    VNVPUFUAUGVKVBVDVEVFVHVDVEVFVIWAWDUHVBVGVJUNVBVDVEVFVJUIZVBVDVEVFVJUJZVBVDV
    EVFVJUKZVBVGVHVIULWFWGWHVBVGVHVIUMVBVDVEKVFVHVDKVEVFVIKKZWAABNZWBNWJWCNUOPZ
    VLLWDWIWKVTVLABCDABCEFUPUQABCDABCEFURUSUTVA $.
    $( [14-Oct-2013] $) $( [6-Oct-2013] $)

  ${
    linecgrand.1 $e |- ( ph -> N e. NN ) $.
    linecgrand.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    linecgrand.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    linecgrand.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    linecgrand.5 $e |- ( ph -> P e. ( EE ` N ) ) $.
    linecgrand.6 $e |- ( ph -> Q e. ( EE ` N ) ) $.
    linecgrand.7 $e |- ( ( ph /\ ps ) -> A =/= B ) $.
    linecgrand.8 $e |- ( ( ph /\ ps ) -> A Colinear <. B , C >. ) $.
    linecgrand.9 $e |- ( ( ph /\ ps ) -> <. A , P >. Cgr <. A , Q >. ) $.
    linecgrand.10 $e |- ( ( ph /\ ps ) -> <. B , P >. Cgr <. B , Q >. ) $.
    $( Deduction form of ~ linecgr .  (Contributed by Scott Fenton,
       14-Oct-2013.) $)
    linecgrand $p |- ( ( ph /\ ps ) -> <. C , P >. Cgr <. C , Q >. ) $=
      ( cop wcel wa wne ccolin wbr ccgr jca wi cee cfv linecgr syl132anc adantr
      cn mp2and ) ABUAZCDUBZCDESUCUDZUAZCFSCGSUEUDZDFSDGSUEUDZUAZEFSEGSUEUDZUOU
      PUQOPUFUOUSUTQRUFAURVAUAVBUGZBAHUMTCHUHUIZTDVDTEVDTFVDTGVDTVCIJKLMNCDEFGH
      UJUKULUN $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)
  $}

  $( Identity law for points on lines.  Theorem 4.18 of [Schwabhauser] p. 38.
     (Contributed by Scott Fenton, 7-Oct-2013.) $)
  lineid $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( ( ( A =/= B /\ A Colinear <. B , C >. ) /\
        ( <. A , C >. Cgr <. A , D >. /\ <. B , C >. Cgr <. B , D >. ) ) ->
      C = D ) ) $=
    ( cn wcel cee cfv wa w3a wne cop ccolin wbr ccgr wceq wi simp2l simp2r 3jca
    simp3l linecgr syld3an2 simp1 simp3r cgrid2 syl13anc syld ) EFGZAEHIZGZBUKG
    ZJZCUKGZDUKGZJZKZABLABCMZNOJACMADMPOUSBDMPOJJZCCMCDMPOZCDQZUJULUMUOKUNUQUTV
    ARURULUMUOUJULUMUQSUJULUMUQTUJUNUOUPUBZUAABCCDEUCUDURUJUOUOUPVAVBRUJUNUQUEV
    CVCUJUNUOUPUFCCDEUGUHUI $.
    $( [14-Oct-2013] $) $( [7-Oct-2013] $)

  $( Law for finding a point inside a segment.  Theorem 4.19 of [Schwabhauser]
     p. 38.  (Contributed by Scott Fenton, 7-Oct-2013.) $)
  idinside $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
   ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
   ( ( C Btwn <. A , B >. /\ <. A , C >. Cgr <. A , D >. /\
       <. B , C >. Cgr <. B , D >. ) -> C = D ) ) $=
    ( cn wcel wa w3a cop cbtwn wbr ccgr wceq wi simp1 syl13anc opeq1 imbi1d idd
    cee simp3l simp3r cgrid2 axbtwnid syl3anc breq12d biimpcd ax-1 syl8 sylsyld
    cfv simp2l 3impd opeq2 breq2d 3anbi1d syl5ib ccolin simpr2l simpr2r simpr3l
    wne simpr1 btwncolinear1 3anim123d anim2i 3simpc adantl lineid exp3a impcom
    jca syl5 syld ex pm2.61ine ) EFGZAEUAULZGZBVSGZHZCVSGZDVSGZHZIZCABJZKLZACJZ
    ADJZMLZBCJZBDJMLZIZCDNZOZOABWFCAAJZKLZWKWMIZWOOABNZWPWFWRWKWMWOWFCCJZCDJZML
    ZWOOZWRCANZWKWMWOOZOWFVRWCWCWDXDVRWBWEPZVRWBWCWDUBZXHVRWBWCWDUCCCDEUDQWFVRW
    CVTWRXEOXGXHVRVTWAWEUMCAEUEUFXDXEWKWOXFXEXDWKWOOXEXCWKWOXEXAWIXBWJMCACRCADR
    UGSUHWOWMUIUJUKUNWTWSWNWOWTWRWHWKWMWTWQWGCKABAUOUPUQSURABVCZWFWPXIWFHZWNAWL
    USLZWKWMIZWOXJWHXKWKWKWMWMXJVRVTWAWCWHXKOXIVRWBWEVDVTWAVRWEXIUTVTWAVRWEXIVA
    WCWDVRWBXIVBABCEVEQXJWKTXJWMTVFWFXIXLWOOWFXIXLWOXIXLHZXIXKHZWKWMHZHWFWOXMXN
    XOXLXKXIXKWKWMPVGXLXOXIXKWKWMVHVIVMABCDEVJVNVKVLVOVPVQ $.
    $( [14-Oct-2013] $) $( [7-Oct-2013] $)

  $( If ` A ` , ` B ` , and ` C ` fall in order on a line, and ` A B ` and
     ` A C ` are congruent, then ` C = B ` .  (Contributed by Scott Fenton,
     7-Oct-2013.) $)
  endofsegid $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( ( B Btwn <. A , C >. /\ <. A , C >. Cgr <. A , B >. ) ->
      C = B ) ) $=
    ( cn wcel cee cfv w3a wa cop cbtwn ccgr wceq wb simpl syl3an3br 3anidm23 wi
    wbr ccgr3 simpr1 simpr3 simpr2 cgrcom syl122anc idd axcgrrflx 3adant3r1 a1d
    biimpd 3jcad 3ancomb brcgr3 sylibrd btwnxfr sylan2d jcad 3anrot btwnswapid2
    a1i sylan2br syld ) DEFZADGHZFZBVEFZCVEFZIZJZBACKZLTZVKABKZMTZJZCVMLTZVLJZC
    BNZVJVOVPVLVJVNABCKZKACBKZKUATZVLVPVJVNVMVKMTZVNVSVTMTZIZWAVJVNWBVNWCVJVNWB
    VJVDVFVHVFVGVNWBOVDVIPVDVFVGVHUBZVDVFVGVHUCWEVDVFVGVHUDACABDUEUFUKVJVNUGVJW
    CVNVDVGVHWCVFBCDUHUIUJULVDVIWAWDOZVIVDVIVFVHVGIZWFVFVHVGUMZABCACBDUNQRUOVDV
    IVLWAJVPSZVIVDVIWGWIWHABCACBDUPQRUQVOVLSVJVLVNPVAURVIVDVHVFVGIVQVRSVHVFVGUS
    CABDUTVBVC $.
    $( [14-Oct-2013] $) $( [7-Oct-2013] $)

  ${
    endofsegidand.1 $e |- ( ph -> N e. NN ) $.
    endofsegidand.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    endofsegidand.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    endofsegidand.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    endofsegidand.5 $e |- ( ( ph /\ ps ) -> C Btwn <. A , B >. ) $.
    endofsegidand.6 $e |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. A , C >. ) $.
    $( Deduction form of ~ endofsegid .  (Contributed by Scott Fenton,
       15-Oct-2013). $)
    endofsegidand $p |- ( ( ph /\ ps ) -> B = C ) $=
      ( wa cop cbtwn wbr ccgr wceq wi wcel cn endofsegid syl13anc adantr mp2and
      cee cfv ) ABMECDNZOPZUHCENQPZDERZKLAUIUJMUKSZBAFUATCFUFUGZTEUMTDUMTULGHJI
      CEDFUBUCUDUE $.
      $( [19-Apr-2014] $) $( [15-Oct-2013] $)
  $}

