$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Segment Transportation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TransportTo $.

  $( Declare the syntax for the segment transport function. $)
  ctransport $a class TransportTo $.

  ${
    $d n p q r x $.
    $( Define the segment transport function.  See ~ fvtransport for an
       explanation of the function. $)
    df-transport $a |- TransportTo =
       { <. <. p , q >. , x >. |
         E. n e. NN ( ( p e. ( ( EE ` n ) X. ( EE ` n ) ) /\
                        q e. ( ( EE ` n ) X. ( EE ` n ) ) /\
                        ( 1st ` q ) =/= ( 2nd ` q ) ) /\
                      x = ( iota_ r e. ( EE ` n )
                        ( ( 2nd ` q ) Btwn <. ( 1st ` q ) , r >. /\
                        <. ( 2nd ` q ) , r >. Cgr p ) ) ) } $.
  $}

  ${
    $d m n p q r x y $.
    $( The ` TransportTo ` relationship is a function. $)
    funtransport $p |- Fun TransportTo $=
      ( vp vn vq vx vr vm vy ctransport wfun cv cee cfv cxp wcel c1st w3a wa cn
      wceq wrex c2nd wne cop cbtwn wbr ccgr crio copab2 wmo weq wi reeanv simp1
      anim12i anim1i an4s xp1st axdimuniq fveq2 riotaeqdv eqeq2d anbi2d syl6bir
      wal eqtr3 syl ex syl2ani imp3a syl5 rexlimivv sylbir eqeq1 rexbidv xpeq12
      gen2 syl2anc eleq2d 3anbi12d anbi12d cbvrexv syl6bb funoprab df-transport
      mo4 mpbir funeqi ) HIAJZBJZKLZWJMZNZCJZWKNZWMOLZWMUALZUBZPZDJZWPWOEJZUCUD
      UEWPWTUCWHUFUEQZEWJUGZSZQZBRTZACDUHZIXEACDXEDUIXEWHFJZKLZXHMZNZWMXINZWQPZ
      GJZXAEXHUGZSZQZFRTZQZDGUJZUKZGVDDVDXTDGXRXDXPQZFRTBRTXSXDXPBFRRULYAXSBFRR
      YAWLXJQZXCXOQZQZWIRNZXGRNZQZXSWRXLXCXOYDWRXLQYBYCWRWLXLXJWLWNWQUMXJXKWQUM
      UNUOUPYGYBYCXSWLYGWHOLZWJNZYHXHNZYCXSUKZXJWHWJWJUQWHXHXHUQYGYIYJQYKYEYIYF
      YJYKYEYIQYFYJQQBFUJZYKYHXGWIURYLYCXCXMXBSZQXSYLYMXOXCYLXBXNXMYLXAEWJXHWIX
      GKUSZUTVAZVBWSXMXBVEVCVFUPVGVHVIVJVKVLVPXEXQDGXSXEWRYMQZBRTXQXSXDYPBRXSXC
      YMWRWSXMXBVMVBVNYPXPBFRYLWRXLYMXOYLWLXJWNXKWQYLWKXIWHYLWJXHSZYQWKXISYNYNW
      JXHWJXHVOVQZVRYLWKXIWMYRVRVSYOVTWAWBWEWFWCHXFDBECAWDWGWF $.
      $( [19-Apr-2014] $) $( [18-Oct-2013] $)
  $}

  ${
    $d N n p q r x $.  $d A n p q r x $.  $d B n p q r x $.  $d C n p q r x $.
    $d D n p q r x $.
    $( Calculate the value of the ` TransportTo ` function.  This function
       takes four points, ` A ` through ` D ` , where ` C ` and ` D ` are
       distinct.  It then returns the point that extends ` C D ` by the length
       of ` A B ` .  (Contributed by Scott Fenton, 18-Oct-2013.) $)
    fvtransport $p |- ( ( N e. NN /\
       ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) /\
         C =/= D ) ) ->
       ( <. A , B >. TransportTo <. C , D >. ) =
       ( iota_ r e. ( EE ` N ) ( D Btwn <. C , r >. /\
         <. D , r >. Cgr <. A , B >. ) ) ) $=
      ( vn cn wcel cfv wa w3a cop ctransport cv cbtwn wbr ccgr wceq cvv cee wne
      vp vq vx co crio df-ov cxp c1st c2nd wrex opelxpi 3ad2ant1 3ad2ant2 simp3
      simp2l op1stg op2ndg 3netr4d 3jca opeq1d breq12d breq1d anbi12d riotabidv
      syl eqcomd jca fveq2 xpeq12 syl2anc eleq2d 3anbi12d eqeq2d rcla4ev sylan2
      riotaeqdv copab2 df-br df-transport eleq2i wb riotaex eleq1 3anbi1d breq2
      opex anbi2d rexbidv neeq12d 3anbi23d eqeq1 mp3an 3bitri wfun funtransport
      eloprabg wi funbrfv ax-mp sylbir syl5eq ) EHIZAEUAJZIBXEIKZCXEIZDXEIZKZCD
      UBZLZKZABMZCDMZNUFXMXNMZNJZDCFOZMZPQZDXQMZXMRQZKZFXEUGZXMXNNUHXLXMGOZUAJZ
      YEUIZIZXNYFIZXNUJJZXNUKJZUBZLZYCYJYIXQMZPQZYJXQMZXMRQZKZFYEUGZSZKZGHULZXP
      YCSZXKXDXMXEXEUIZIZXNUUCIZYKLZYCYQFXEUGZSZKZUUAXKUUFUUHXKUUDUUEYKXFXIUUDX
      JABXEXEUMUNXIXFUUEXJCDXEXEUMUOXKCDYIYJXFXIXJUPXKXGYICSXFXGXHXJUQCDXEURVGZ
      XIXFYJDSXJCDXEXEUSUOZUTVAXKUUGYCXKYQYBFXEXKYNXSYPYAXKYJDYMXRPUUKXKYICXQUU
      JVBVCXKYOXTXMRXKYJDXQUUKVBVDVEVFVHVIYTUUIGEHYDESZYLUUFYSUUHUULYGUUDYHUUEY
      KUULYFUUCXMUULYEXESZUUMYFUUCSYDEUAVJZUUNYEXEYEXEVKVLZVMUULYFUUCXNUUOVMVNU
      ULYRUUGYCUULYQFYEXEUUNVRVOVEVPVQUUAXOYCNQZUUBUUPXOYCMZNIUUQUCOZYFIZUDOZYF
      IZUUTUJJZUUTUKJZUBZLZUEOZUVCUVBXQMZPQZUVCXQMZUURRQZKZFYEUGZSZKZGHULZUCUDU
      EVSZIZUUAXOYCNVTNUVPUUQUEGFUDUCWAWBXMTIXNTIYCTIUVQUUAWCABWHCDWHYBFXEWDZUV
      OYGUVAUVDLZUVFUVHUVIXMRQZKZFYEUGZSZKZGHULYLUVFYRSZKZGHULUUAUCUDUEXMXNYCTT
      TUURXMSZUVNUWDGHUWGUVEUVSUVMUWCUWGUUSYGUVAUVDUURXMYFWEWFUWGUVLUWBUVFUWGUV
      KUWAFYEUWGUVJUVTUVHUURXMUVIRWGWIVFVOVEWJUUTXNSZUWDUWFGHUWHUVSYLUWCUWEUWHU
      VAYHUVDYKYGUUTXNYFWEUWHUVBYIUVCYJUUTXNUJVJZUUTXNUKVJZWKWLUWHUWBYRUVFUWHUW
      AYQFYEUWHUVHYNUVTYPUWHUVCYJUVGYMPUWJUWHUVBYIXQUWIVBVCUWHUVIYOXMRUWHUVCYJX
      QUWJVBVDVEVFVOVEWJUVFYCSZUWFYTGHUWKUWEYSYLUVFYCYRWMWIWJWRWNWONWPUUPUUBWSW
      QXOYCNUVRWTXAXBVGXC $.
      $( [19-Apr-2014] $) $( [18-Oct-2013] $)
  $}

  ${
    $d N r y $.  $d A r y $.  $d B r y $.  $d C r y $.  $d D r y $.
    $( Closure law for segment transport.  (Contributed by Scott Fenton,
       19-Oct-2013.) $)
    transportcl $p |- ( ( N e. NN /\
       ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) /\
         C =/= D ) ) ->
       ( <. A , B >. TransportTo <. C , D >. ) e. ( EE ` N ) ) $=
      ( vr cn wcel cee cfv wa wne w3a cop ctransport co cv cbtwn wbr ccgr crio
      fvtransport wreu segconeu riotacl syl eqeltrd ) EGHAEIJZHBUHHKCUHHDUHHKCD
      LMKZABNZCDNOPDCFQZNRSDUKNUJTSKZFUHUAZUHABCDEFUBUIULFUHUCUMUHHABCDEFUDULFU
      HUEUFUG $.
      $( [19-Apr-2014] $) $( [19-Oct-2013] $)

    $( Calculate the defining properties of the transport function (Contributed
       by Scott Fenton, 19-Oct-2013.) $)
    transportprops $p |- ( ( N e. NN /\
       ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) /\
         C =/= D ) ) ->
         ( D Btwn <. C , ( <. A , B >. TransportTo <. C , D >. ) >. /\
           <. D , ( <. A , B >. TransportTo <. C , D >. ) >. Cgr
           <. A , B >. ) ) $=
      ( vr vy cn wcel cee cfv wa wne cop cbtwn wbr ccgr cv wceq opeq2 co eqcomd
      w3a ctransport crio fvtransport wreu wb transportcl segconeu ax-17 breq2d
      a17d breq1d anbi12d riota2f syl2anc mpbird ) EHIAEJKZIBUSILCUSIDUSILCDMUC
      LZDCABNZCDNUDUAZNZOPZDVBNZVAQPZLZDCFRZNZOPZDVHNZVAQPZLZFUSUEZVBSZUTVBVNAB
      CDEFUFUBUTVBUSIZVMFUSUGVGVOUHABCDEUIABCDEFUJVMVGFGUSVBGRVBIFUKVPVGFUMVHVB
      SZVJVDVLVFVQVIVCDOVHVBCTULVQVKVEVAQVHVBDTUNUOUPUQUR $.
      $( [19-Apr-2014] $) $( [19-Oct-2013] $)

  $}

