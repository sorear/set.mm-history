$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              The Predecessor Class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c Pred $.
  $( The predecessors symbol. $)
  cpred $a class Pred ( R , A , X ) $.

  $( Define the predecessor class of a relationship.  This is the class of all
     elements ` y ` of ` A ` such that ` y R X ` (see ~ elpred ) . $)
  df-pred $a |- Pred ( R , A , X ) = ( A i^i ( `' R " { X } ) ) $.

  $( Equality theorem for the predecessor class.  (Contributed by Scott Fenton,
     2-Feb-2011.) $)
  predeq1 $p |- ( R = S -> Pred ( R , A , X ) = Pred ( S , A , X ) ) $=
    ( wceq ccnv csn cima cin cpred cnveq imaeq1d ineq2d df-pred 3eqtr4g ) BCEZA
    BFZDGZHZIACFZRHZIABDJACDJPSUAAPQTRBCKLMABDNACDNO $.
    $( [2-Feb-2011] $)

  $( Equality theorem for the predecessor class.  (Contributed by Scott Fenton,
     2-Feb-2011.) $)
  predeq2 $p |- ( A = B -> Pred ( R , A , X ) = Pred ( R , B , X ) ) $=
    ( wceq ccnv csn cima cin cpred ineq1 df-pred 3eqtr4g ) ABEACFDGHZIBNIACDJBC
    DJABNKACDLBCDLM $.
    $( [2-Feb-2011] $)

  $( Equality theorem for the predecessor class.  (Contributed by Scott Fenton,
     2-Feb-2011.) $)
  predeq3 $p |- ( X = Y -> Pred ( R , A , X ) = Pred ( R , A , Y ) ) $=
    ( wceq ccnv csn cima cin cpred sneq imaeq2d ineq2d df-pred 3eqtr4g ) CDEZAB
    FZCGZHZIAQDGZHZIABCJABDJPSUAAPRTQCDKLMABCNABDNO $.
    $( [2-Feb-2011] $)

  $( If ` A ` is a subset of ` B ` , then their predecessor classes are also
     subsets.  (Contributed by Scott Fenton, 2-Feb-2011.) $)
  predpredss $p |- ( A C_ B -> Pred ( R , A , X ) C_ Pred ( R , B , X ) ) $=
    ( wss ccnv csn cima cin cpred ssrin df-pred 3sstr4g ) ABEACFDGHZIBNIACDJBCD
    JABNKACDLBCDLM $.
    $( [2-Feb-2011] $)

  $( The predecessor class of ` A ` is a subset of ` A ` (Contributed by Scott
     Fenton, 2-Feb-2011.) $)
  predss $p |- Pred ( R , A , X ) C_ A $=
    ( cpred ccnv csn cima cin df-pred inss1 eqsstri ) ABCDABECFGZHAABCIALJK $.
    $( [2-Feb-2011] $)

  $( Another subset/predecessor class relationship.  (Contributed by Scott
     Fenton, 6-Feb-2011.) $)
  sspred $p |- ( ( B C_ A /\ Pred ( R , A , X ) C_ B ) ->
                 Pred ( R , A , X ) = Pred ( R , B , X ) ) $=
    ( wss cin wceq ccnv csn cima cpred sseqin2 df-pred sseq1i df-ss in32 eqeq1i
    3bitri wa ineq1 eqeq1d biimpa eqeq12i biimpri eqcoms syl syl2anb ) BAEABFZB
    GZUHCHDIJZFZAUJFZGZACDKZBCDKZGZUNBEZBALUQULBEULBFZULGUMUNULBACDMZNULBOURUKU
    LAUJBPQRUIUMSBUJFZULGZUPUIUMVAUIUKUTULUHBUJTUAUBUPULUTUPULUTGUNULUOUTUSBCDM
    UCUDUEUFUG $.
    $( [6-Feb-2011] $)

  ${
    $d R y $.  $d X y $.
    dfpred2.1 $e |- X e. _V $.
    $( An alternate definition of predecessor class when ` X ` is a set.
       (Contributed by Scott Fenton, 8-Feb-2011.) $)
    dfpred2 $p |- Pred ( R , A , X ) = ( A i^i { y | y R X } ) $=
      ( cpred ccnv csn cima cin cv wbr cab df-pred cvv wcel iniseg ax-mp ineq2i
      wceq eqtri ) BCDFBCGDHIZJBAKDCLAMZJBCDNUBUCBDOPUBUCTEACDOQRSUA $.
      $( [8-Feb-2011] $)
  $}

  ${
    elpredim.1 $e |- X e. _V $.
    $( Membership in a predecessor class - implicative version.  (Contributed
       by Scott Fenton, 9-May-2012.) $)
    elpredim $p |- ( Y e. Pred ( R , A , X ) -> Y R X ) $=
      ( cpred wcel ccnv csn cima wa wbr cin df-pred eleq2i elin bitri cop cvv
      wb elimasng opelcnvg bitrd mpan ibi df-br sylibr adantl sylbi ) DABCFZGZD
      AGZDBHZCIJZGZKZDCBLZUKDAUNMZGUPUJURDABCNODAUNPQUOUQULUODCRBGZUQUOUSCSGZUO
      UOUSTEUTUOKUOCDRUMGUSUMCDSUNUACDSUNBUBUCUDUEDCBUFUGUHUI $.
      $( [12-May-2012] $) $( [9-May-2012] $)
  $}

  ${
    elpred.1 $e |- Y e. _V $.
    $( Membership in a predecessor class.  (Contributed by Scott Fenton,
       4-Feb-2011.) $)
    elpred $p |- ( X e. D -> ( Y e. Pred ( R , A , X ) <->
                                ( Y e. A /\ Y R X ) ) ) $=
      ( cpred wcel ccnv csn cima wa wbr cin df-pred eleq2i elin eliniseg anbi2d
      bitri syl5bb ) EACDGZHZEAHZECIDJKZHZLZDBHZUDEDCMZLUCEAUENZHUGUBUJEACDOPEA
      UEQTUHUFUIUDCDEBFRSUA $.
      $( [4-Feb-2011] $)
  $}

  $( Membership in a predecessor class.  (Contributed by Scott Fenton,
     17-Apr-2011.) $)
  elpredg $p |- ( ( X e. B /\ Y e. A ) ->
                  ( Y e. Pred ( R , A , X ) <-> Y R X ) ) $=
    ( wcel wa cpred ccnv csn cima wbr cin df-pred eleq2i elin bitri baib adantl
    wb cop elimasng df-br a1i brcnvg 3bitr2d bitrd ) DBFZEAFZGZEACDHZFZECIZDJKZ
    FZEDCLZUIULUOTUHULUIUOULEAUNMZFUIUOGUKUQEACDNOEAUNPQRSUJUODEUAUMFZDEUMLZUPU
    MDEBAUBUSURTUJDEUMUCUDDEBACUEUFUG $.
    $( [17-Apr-2011] $)

  ${
    $d A y $.  $d F y $.  $d G y $.  $d X y $.  $d R y $.
    predreseq.1 $e |- X e. _V $.
    $( Equality of restriction to predecessor classes.  (Contributed by Scott
       Fenton, 8-Feb-2011.) $)
    predreseq $p |- ( ( F Fn A /\ G Fn A ) ->
                       ( ( F |` Pred ( R , A , X ) ) =
                         ( G |` Pred ( R , A , X ) ) <->
                         A. y e. A ( y R X -> ( F ` y ) = ( G ` y ) ) ) ) $=
      ( wfn wa cres wceq cfv wral wi wb wcel wal df-ral cvv albii cpred wbr wss
      predss fvreseq mpan2 vex elpred ax-mp imbi1i impexp 3bitri bitr4i syl6bb
      cv ) DBHEBHIZDBCFUAZJEUQJKZAUOZDLUSELKZAUQMZUSFCUBZUTNZABMZUPUQBUCURVAOBC
      FUDABUQDEUEUFVAUSBPZVCNZAQZVDVAUSUQPZUTNZAQVEVBIZUTNZAQVGUTAUQRVIVKAVHVJU
      TFSPVHVJOGBSCFUSAUGUHUIUJTVKVFAVEVBUTUKTULVCABRUMUN $.
      $( [8-Feb-2011] $)
  $}

  ${
    predasetex.1 $e |- A e. _V $.
    $( The predecessor class exists when ` A ` does.  (Contributed by Scott
       Fenton, 8-Feb-2011.) $)
    predasetex $p |- Pred ( R , A , X ) e. _V $=
      ( cpred ccnv csn cima cin cvv df-pred inex1 eqeltri ) ABCEABFCGHZIJABCKAN
      DLM $.
      $( [8-Feb-2011] $)
  $}

  ${
    $d R x z $.  $d R y z $.  $d A x z $.  $d A y z $.
    $( Change the bound variable in the statement stating that ` R ` is
       set-like.  (Contributed by Scott Fenton, 2-Feb-2011.) $)
    cbvsetlike $p |- ( A. x e. A Pred ( R , A , x ) e. _V <->
                        A. y e. A Pred ( R , A , y ) e. _V ) $=
      ( vz cv cpred cvv wcel wral weq predeq3 eleq1d cbvralv bitr4i ) CDAFZGZHI
      ZACJCDEFZGZHIZECJCDBFZGZHIZBCJRUAAECAEKQTHCDPSLMNUDUABECBEKUCTHCDUBSLMNO
      $.
      $( [2-Feb-2011] $)
  $}

  ${
    $d x y R $.  $d x A $.
    $( Alternate definition of founded relation.  (Contributed by Scott Fenton,
       2-Feb-2011.) $)
    dffr4 $p |- ( R Fr A <->
                   A. x ( ( x C_ A /\ x =/= (/) )
                          -> E. y e. x Pred ( R , x , y ) = (/) ) ) $=
      ( wfr cv wss c0 wne wa ccnv csn cima cin wceq wrex wi wal cpred dffr3
      df-pred eqeq1i rexbii imbi2i albii bitr4i ) CDEAFZCGUGHIJZUGDKBFZLMNZHOZB
      UGPZQZARUHUGDUISZHOZBUGPZQZARABCDTUQUMAUPULUHUOUKBUGUNUJHUGDUIUAUBUCUDUEU
      F $.
      $( [2-Feb-2011] $)
  $}

  ${
    $( Membership in the predecessor class implies membership in the base
       class.  (Contributed by Scott Fenton, 11-Feb-2011.) $)
    predel $p |- ( Y e. Pred ( R , A , X ) -> Y e. A ) $=
      ( wcel ccnv csn cima cin cpred elin simplbi df-pred eleq2s ) DAEZDABFCGHZ
      IZABCJDQEODPEDAPKLABCMN $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d R z $.  $d A z $.  $d X z $.  $d Y z $.
    $( Property of the precessor class for partial orderings.  (Contributed by
       Scott Fenton, 28-Apr-2012.) $)
    predpo $p |- ( ( R Po A /\ X e. A ) ->
                   ( Y e. Pred ( R , A , X ) ->
                     Pred ( R , A , Y ) C_ Pred ( R , A , X ) ) ) $=
      ( vz wpo wcel wa cpred wss predel w3a cv wbr wi wb elpredg adantll elpred
      ex potr 3exp2 com24 imp31 com13 com14 sylbid com23 imdistand vex 3ad2ant3
      3imp adantl 3ad2ant1 3imtr4d ssrdv 3exp mpdi ) ABFZCAGZHZDABCIZGZDAGZABDI
      ZVBJZABCDKVAVCVDVFVAVCVDLZEVEVBVGEMZAGZVHDBNZHZVIVHCBNZHZVHVEGZVHVBGZVGVI
      VJVLVAVCVDVIVJVLOOZVAVDVCVPVAVDVCVPOVAVDHZVCDCBNZVPUTVDVCVRPUSAABCDQRVJVR
      VIVQVLVJVRVIVQVLOOVQVIVJVRHZVLUSUTVDVIVSVLOZOUSVIVDUTVTUSVIVDUTVTAVHDCBUA
      UBUCUDUETUFUGTUHULUIVDVAVNVKPVCAABDVHEUJZSUKVAVCVOVMPZVDUTWBUSAABCVHWASUM
      UNUOUPUQUR $.
      $( [12-May-2012] $) $( [28-Apr-2012] $)
  $}

  $( Property of the predecessor class for strict orderings.  (Contributed by
     Scott Fenton, 11-Feb-2011.) $)
  predso $p |- ( ( R Or A /\ X e. A ) ->
                  ( Y e. Pred ( R , A , X ) ->
                    Pred ( R , A , Y ) C_ Pred ( R , A , X ) ) ) $=
    ( wor wpo wcel cpred wss wi sopo predpo sylan ) ABEABFCAGDABCHZGABDHNIJABKA
    BCDLM $.
    $( [11-Feb-2011] $)

  ${
    predbr.1 $e |- X e. _V $.
    predbr.2 $e |- Y e. _V $.
    $( If a set is in the predecessor class, then it is less than the base
       element.  (Contributed by Scott Fenton, 11-Feb-2011.) $)
    predbr $p |- ( Y e. Pred ( R , A , X ) -> Y R X ) $=
      ( cpred wcel wbr wa cvv wb elpred ax-mp biimpi simprd ) DABCGHZDAHZDCBIZQ
      RSJZCKHQTLEAKBCDFMNOP $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d A x y $.  $d R x y $.  $d X x y $.  $d Y y $.
    $( Closed form of ~ predbr .  (Contributed by Scott Fenton,
       13-Apr-2011.) $)
    predbrg $p |- ( ( X e. A /\ Y e. B ) ->
                     ( Y e. Pred ( R , A , X ) -> Y R X ) ) $=
      ( vy vx cv cpred wcel wbr wi predeq3 eleq2d breq2 imbi12d eleq1 breq1 vex
      wceq predbr vtocl2g ) FHZACGHZIZJZUCUDCKZLUCACDIZJZUCDCKZLEUHJZEDCKZLGFDE
      ABUDDTZUFUIUGUJUMUEUHUCACUDDMNUDDUCCOPUCETUIUKUJULUCEUHQUCEDCRPACUDUCGSFS
      UAUB $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d R x $.  $d A x $.  $d X x $.
    $( If ` R ` is set-like in ` A ` then all predecessors classes of elements
       of ` A ` exist.  (Contributed by Scott Fenton, 20-Feb-2011.) $)
    setlikespec $p |- ( ( X e. A /\ A. x e. A Pred ( R , A , x ) e. _V )
                         -> Pred ( R , A , X ) e. _V ) $=
      ( cv cpred cvv wcel wceq predeq3 eleq1d rcla4va ) BCAEZFZGHBCDFZGHADBMDIN
      OGBCMDJKL $.
      $( [20-Feb-2011] $)
  $}

  $( Idempotent law for the predecessor class.  (Contributed by Scott Fenton,
     29-Mar-2011.) $)
  predidm $p |- Pred ( R , Pred ( R , A , X ) , X ) = Pred ( R , A , X ) $=
    ( cpred ccnv csn cima df-pred ineq1i inidm ineq2i inass 3eqtr4ri 3eqtr4i
    cin ) ABCDZBECFGZOAQOZQOZPBCDPPRQABCHZIPBCHAQQOZORSPUAQAQJKAQQLTMN $.
    $( [29-Mar-2011] $)

  $( Intersection law for predecessor classes.  (Contributed by Scott Fenton,
     29-Mar-2011.) $)
  predin $p |- Pred ( R , ( A i^i B ) , X ) =
               ( Pred ( R , A , X ) i^i Pred ( R , B , X ) ) $=
    ( cin ccnv csn cima cpred inindir df-pred ineq12i 3eqtr4i ) ABEZCFDGHZEAOEZ
    BOEZENCDIACDIZBCDIZEABOJNCDKRPSQACDKBCDKLM $.
    $( [29-Mar-2011] $)

  $( Union law for predecessor classes.  (Contributed by Scott Fenton,
     29-Mar-2011.) $)
  predun $p |- Pred ( R , ( A u. B ) , X ) =
               ( Pred ( R , A , X ) u. Pred ( R , B , X ) ) $=
    ( cun ccnv csn cima cin cpred indir df-pred uneq12i 3eqtr4i ) ABEZCFDGHZIAP
    IZBPIZEOCDJACDJZBCDJZEABPKOCDLSQTRACDLBCDLMN $.
    $( [29-Mar-2011] $)

  $( Difference law for predecessor classes.  (Contributed by Scott Fenton,
     14-Apr-2011.) $)
  preddif $p |- Pred ( R , ( A \ B ) , X ) =
                  ( Pred ( R , A , X ) \ Pred ( R , B , X ) ) $=
    ( cdif ccnv csn cima cin cpred indifdir df-pred difeq12i 3eqtr4i ) ABEZCFDG
    HZIAPIZBPIZEOCDJACDJZBCDJZEABPKOCDLSQTRACDLBCDLMN $.
    $( [14-Apr-2011] $)

  ${
    $d X y $.  $d B y $.
    $( The predecessor under the epsilon relationship is equivalent to an
       intersection.  (Contributed by Scott Fenton, 27-Mar-2011.)  (The proof
       was shortened by Andrew Salmon, 27-Aug-2011.) $)
    predep $p |- ( X e. B -> Pred ( _E , A , X ) = ( A i^i X ) ) $=
      ( vy wcel cep cpred ccnv csn cima cin df-pred cv wbr cab wrel wceq relcnv
      cvv syl5eq relimasn ax-mp wb vex brcnvg mpan2 epelg bitrd abbi1dv ineq2d
      ) CBEZAFCGAFHZCIJZKACKAFCLUKUMCAUKUMCDMZULNZDOZCULPUMUPQFRDCULUAUBUKUODCU
      KUOUNCFNZUNCEUKUNSEUOUQUCDUDCUNBSFUEUFUNCBUGUHUITUJT $.
      $( [27-Aug-2011] $) $( [27-Mar-2011] $)
  $}

  $( For an ordinal, the predecessor under ` _E ` and ` On ` is an identity
     relationship.  (Contributed by Scott Fenton, 27-Mar-2011.) $)
  predon $p |- ( A e. On -> Pred ( _E , On , A ) = A ) $=
    ( con0 wcel cep cpred cin predep wss wceq onss sseqin2 sylib eqtrd ) ABCZBD
    AEBAFZABBAGNABHOAIAJABKLM $.
    $( [27-Mar-2011] $)

  $( The epsilon relationship is set-like.  (Contributed by Scott Fenton,
     27-Mar-2011.) $)
  epsetlike $p |- A. x e. A Pred ( _E , A , x ) e. _V $=
    ( cep cv cpred cvv wcel cin predep incom syl6eq inex1g eqeltrd rgen ) BCADZ
    EZFGABOBGZPOBHZFQPBOHRBBOIBOJKOBBLMN $.
    $( [27-Mar-2011] $)

  ${
    $d A x $.  $d B x $.
    $( If ` R ` is set-like over ` A ` , then it is set-like over any subclass
       of ` A ` .  (Contributed by Scott Fenton, 28-Mar-2011.) $)
    setlikess $p |- ( ( A C_ B /\ A. x e. B Pred ( R , B , x ) e. _V ) ->
                       A. x e. A Pred ( R , A , x ) e. _V ) $=
      ( wss cv cpred cvv wcel wral ssralv predpredss ssexg syl ralimdv syld imp
      wi ex ) BCEZCDAFZGZHIZACJZBDUAGZHIZABJZTUDUCABJUGUCABCKTUCUFABTUEUBEZUCUF
      RBCDUALUHUCUFUEUBHMSNOPQ $.
      $( [28-Mar-2011] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d R x y z $.  $d X y $.
    $( A property of classes that are downward closed under predecessor.
       (Contributed by Scott Fenton, 13-Apr-2011.) $)
    preddowncl $p |- ( ( B C_ A /\ A. x e. B Pred ( R , A , x ) C_ B ) ->
                        ( X e. B ->
                          Pred ( R , B , X ) = Pred ( R , A , X ) ) ) $=
      ( vy vz wss cv cpred wral wa wcel wceq eleq1 predeq3 eqeq12d imbi2d vex
      wi imbi12d predpredss ad2antrr wbr weq sseq1d rcla4cva predbr a1i jcad wb
      sseld elpred adantl mpbird ssrdv adantll eqssd ex vtoclg pm2.43b ) CBHZBD
      AIZJZCHZACKZLZECMZCDEJZBDEJZNZVGFIZCMZCDVLJZBDVLJZNZTZTVGVHVKTZTFECVLENZV
      QVRVGVSVMVHVPVKVLECOVSVNVIVOVJCDVLEPBDVLEPQUARVGVMVPVGVMLVNVOVBVNVOHVFVMC
      BDVLUBUCVFVMVOVNHVBVFVMLZGVOVNVTGIZVOMZWAVNMZTZWBWACMZWAVLDUDZLZTZVTWBWEW
      FVTVOCWAVEVOCHAVLCAFUEVDVOCBDVCVLPUFUGULWBWFTVTBDVLWAFSGSZUHUIUJVMWDWHUKV
      FVMWCWGWBCCDVLWAWIUMRUNUOUPUQURUSUTVA $.
      $( [13-Apr-2011] $)
  $}

  $( Given a partial ordering, ` X ` is not a member of its predecessor class.
     (Contributed by Scott Fenton, 17-Apr-2011.) $)
  predpoirr $p |- ( R Po A -> -. X e. Pred ( R , A , X ) ) $=
    ( wpo wcel cpred wn wa poirr wb elpredg anidms notbid syl5ibr exp3a pm2.43b
    wbr predel con3i pm2.61d1 ) ABDZCAEZCABCFEZGZUAUBUDUBUAUBUDUAUBHUDUBCCBQZGA
    CBIUBUCUEUBUCUEJAABCCKLMNOPUCUBABCCRST $.
    $( [17-Apr-2011] $)

  $( Given a founded relationship, ` X ` is not a member of its predecessor
     class.  (Contributed by Scott Fenton, 22-Apr-2011.) $)
  predfrirr $p |- ( R Fr A -> -. X e. Pred ( R , A , X ) ) $=
    ( wfr cpred wn wa wbr frirrc wb elpredg anidms notbid syl5ibr exp3a pm2.43b
    wcel predel con3i pm2.61d1 ) ABDZCAQZCABCEQZFZUAUBUDUBUAUBUDUAUBGUDUBCCBHZF
    ABCIUBUCUEUBUCUEJAABCCKLMNOPUCUBABCCRST $.
    $( [22-Apr-2011] $)


  $( The predecessor class over ` (/) ` is always ` (/) ` (Contributed by Scott
     Fenton, 16-Apr-2011.) $)
  pred0 $p |- Pred ( R , (/) , X ) = (/) $=
    ( c0 cpred ccnv csn cima cin df-pred incom in0 3eqtri ) CABDCAEBFGZHMCHCCAB
    ICMJMKL $.
    $( [16-Apr-2011] $)

  ${
    $d N x $.  $d M x $.
    $( The value of the predecessor class over an upper integer set.
       (Contributed by Scott Fenton, 16-May-2014.) $)
    preduz $p |- ( N e. ( ZZ>= ` M ) -> Pred ( < , ( ZZ>= ` M ) , N ) = ( M ...
        ( N - 1 ) ) ) $=
      ( vx cuz cfv wcel clt cpred c1 cmin co cz wa cle wbr wb eluzelz syl anass
      cvv cfz cv vex elpred zltlem1 syl2anr pm5.32da eluzel2 eluz1 anbi1d bitrd
      peano2zm jca biantrurd 3bitrd w3a ovex elfz2 ax-mp df-3an anbi1i 3bitr4ri
      anbi2i bitr4i syl6bbr eqrdv ) BADEZFZCVGGBHZABIJKZUAKZVHCUBZVIFZALFZVJLFZ
      MZVLLFZAVLNOZMZVLVJNOZMZMZVLVKFZVHVMVLVGFZVLBGOZMZWAWBVGVGGBVLCUCUDVHWFWD
      VTMWAVHWDWEVTWDVQBLFZWEVTPVHAVLQABQZVLBUEUFUGVHWDVSVTVHVNWDVSPABUHZAVLUIR
      UJUKVHVPWAVHVNVOWIVHWGVOWHBULRUMUNUOWCVNVOVQUPZVRVTMZMZWBVJTFWCWLPBIJUQTV
      LAVJURUSVPVQMZWKMVPVQWKMZMWLWBVPVQWKSWJWMWKVNVOVQUTVAWAWNVPVQVRVTSVCVBVDV
      EVF $.
      $( [16-May-2014] $)
  $}


  $( The value of the predecessor class over the naturals.  (Contributed by
     Scott Fenton, 6-Aug-2013.) $)
  prednn $p |- ( N e. NN -> Pred ( < , NN , N ) = ( 1 ... ( N - 1 ) ) ) $=
    ( cn wcel clt cpred c1 cuz cfv cmin co cfz wceq predeq2 ax-mp elnnuz preduz
    nnuz sylbi syl5eq ) ABCZBDAEZFGHZDAEZFAFIJKJZBUBLUAUCLQBUBDAMNTAUBCUCUDLAOF
    APRS $.
    $( [6-Aug-2013] $)

  $( The value of the predecessor class over ` NN0 ` .  (Contributed by Scott
     Fenton, 9-May-2014.) $)
  prednn0 $p |- ( N e. NN0 -> Pred ( < , NN0 , N ) = ( 0 ... ( N - 1 ) ) ) $=
    ( cn0 clt cpred cc0 c1 cmin co cfz wceq cuz wcel nn0uz predeq2 ax-mp preduz
    cfv syl5eq eleq2s ) BCADZEAFGHIHZJAEKQZBAUBLTUBCADZUABUBJTUCJMBUBCANOEAPRMS
    $.
    $( [9-May-2014] $)

  ${
    $d K x $.  $d M x $.  $d N x $.  $d V x $.
    $( Calculate the predecessor of an integer under a finite set of integers.
       (Contributed by Scott Fenton, 8-Aug-2013.) $)
    predfz $p |- ( ( N e. V /\ K e. ( M ... N ) ) ->
       Pred ( < , ( M ... N ) , K ) = ( M ... ( K - 1 ) ) ) $=
      ( vx wcel cfz co wa clt wbr cz cle ad2antlr syl wb syl2anc cr zre jca w3a
      cpred c1 cmin cv elfzel1 elfzelz peano2zm ad2antrl elfzle1 simprr zltlem1
      3jca mpbid jca32 ad2antrr simprl1 elfzel2g adantr simprl3 simprrl simprl2
      simprrr elfzle2 zlem1lt lelttrd ltle sylc mpbir2and mpbird impbida elpred
      elfz2 vex adantl cvv ovex ax-mp a1i 3bitr4d eqrdv ) CDFZABCGHZFZIZEWCJAUB
      ZBAUCUDHZGHZWEEUEZWCFZWIAJKZIZBLFZWGLFZWILFZUAZBWIMKZWIWGMKZIZIZWIWFFZWIW
      HFZWEWLWTWEWLIZWPWQWRXCWMWNWOWDWMWBWLABCUFNXCALFZWNWDXDWBWLABCUGZNZAUHOWJ
      WOWEWKWIBCUGUIZUMWJWQWEWKWIBCUJUIXCWKWRWEWJWKUKXCWOXDWKWRPZXGXFWIAULZQUNU
      OWEWTIZWJWKXJWJWMCLFZWOUAZWQWICMKZIZWBWJXLXNIPWDWTDWIBCVMUPXJWMXKWOWMWNWO
      WSWEUQWEXKWTDABCURUSZWMWNWOWSWEUTZUMXJWQXMWEWPWQWRVAXJWIRFZCRFZIWICJKXMXJ
      XQXRXJWOXQXPWISOZXJXKXRXOCSOZTXJWIWGCXSXJWNWGRFWMWNWOWSWEVBWGSOXTWEWPWQWR
      VCZXJACMKZWGCJKZWDYBWBWTABCVDNXJXDXKYBYCPWDXDWBWTXENZXOACVEQUNVFWICVGVHTV
      IXJWKWRYAXJWOXDXHXPYDXIQVJTVKWDXAWLPWBWCWCJAWIEVNVLVOXBWTPZWEWGVPFYEAUCUD
      VQVPWIBWGVMVRVSVTWA $.
      $( [8-Aug-2013] $)
  $}

