$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Neighborhood bases determine topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c d f g h j k m n r s t u v w x y z B $.
    $d d g j k m n o r s t v F $.  $d a b c d g j k m n o r s t v x z G $.
    $d a b c d g j k m n o r s u v y U $.  $d a b c d g m r s t w z U $.
    $d a b c d f g h j k m n o r s t u v x y z X $.
    $d a b c d f g h j k m n o r s t u v w x z W $.  $d g m n u v $.
    $d a b m s t u v w $.  $d a b m o s u w $.  $d v x $.  $d a b m s u y $.
    neibastop1.1 $e |- ( x = y -> U = W ) $.
    $( A collection of neighborhood bases determines a topology.  Part of
       Theorem 4.5 of Stephen Willard's _General Topology_.  (Contributed by
       Jeff Hankins, 8-Sep-2009.) $)
    neibastop1 $p |- ( ( X e. B /\ A. x e. X ( U =/= (/) /\ A. v e. U ( ( x e.
    v /\ v C_ X ) /\ ( A. w e. U E. t e. U t C_ ( v i^i w ) /\ E. t e. U A. y
    e. t E. w e. W w C_ v ) ) ) ) -> { o | ( o C_ X /\ A. x e. o E. v e. U v C_
    o ) } e. Top ) $=
      ( va wcel cv wss wa wrex wral wi sseq1 vs vu vb wne cin cab ctop cuni wal
      simprl ralimi adantl ssinss1 ad2antrr a1i df-ral anbi12i 19.26 bitr4i w3a
      c0 inss1 sseli inss2 ssel syl5com 3jcad pm2.27 3ad2ant1 3ad2ant2 3ad2ant3
      a1d cbvrexv wceq ineq1 sseq2d rexbidv ineq2 rcla42v mpan2 sylan anim12dan
      sstr ssin sylib rcla4ev exp45 rexlimiv syl6 com34 com25 sylbi com14 com12
      sylan2 ex com24 com23 imp4c com3l syl5bir expdimp sylan2b anassrs 3imtr4g
      syld alimdv adantrl jcad sseq2 raleqbi1dv anbi12d inex1 ralrimivv simplbi
      vex elab ralrimiv unissb ax-17 hbra1 hban hbab hbss wex eluni ra4 elssuni
      sylibr sstr2 reximdv syl9r adantld syl5bi imp3a exlimdv ralrimi uniex syl
      cvv sylanbrc ax-gen jctil wb abssexg istopg adantr mpbird ) JFMZGVAUDZANZ
      DNZMUULJOPZENZUULCNZUEZOZEGQZCGRZUUOUULOCIQBUUNREGQZPPZDGRZPZAJRZPHNZJOZU
      ULUVEOZDGQZAUVERZPZHUFZUGMZUANZUVKOZUVMUHZUVKMZSZUAUIZUVMUBNZUEZUVKMZUBUV
      KRUAUVKRZPZUVDUWCUUIUVDUUSDGRZAJRZUWCUVCUWDAJUVBUWDUUJUVAUUSDGUUMUUSUUTUJ
      UKULUKUWEUWBUVRUWEUWAUAUBUVKUVKUWEUVMJOZUULUVMOZDGQZAUVMRZPZUVSJOZUULUVSO
      ZDGQZAUVSRZPZPZUVTJOZUULUVTOZDGQZAUVTRZPZUVMUVKMZUVSUVKMZPUWAUWEUWPUWQUWT
      UWPUWQSUWEUWFUWQUWIUWOUVMUVSJUMUNUOUWPUWEUWTUWJUWNUWEUWTSUWKUWJUWNPUUKJMZ
      UWDSZAUIZUUKUVTMZUWSSZAUIZUWEUWTUWFUWIUWNUXFUXISZUWIUWNPZUWFUUKUVMMZUWHSZ
      UUKUVSMZUWMSZPZAUIZUXJUXKUXMAUIZUXOAUIZPUXQUWIUXRUWNUXSUWHAUVMUPUWMAUVSUP
      UQUXMUXOAURUSUWFUXQUXFUXIUXQUXFPUXPUXEPZAUIUWFUXIUXPUXEAURUWFUXTUXHAUXGUW
      FUXTUWSUXGUWFUXLUXNUXDUTZUXTUWSSUXGUWFUXLUXNUXDUXGUXLUWFUVTUVMUUKUVMUVSVB
      VCZVLUXGUXNUWFUVTUVSUUKUVMUVSVDVCVLUXGUXLUWFUXDUYBUVMJUUKVEVFVGUYAUXMUXOU
      XEUWSUYAUXMUWHUXOUXEUWSSZSUXLUXNUXMUWHSUXDUXLUWHVHVIUYAUXOUWHUYCUYAUXOUWM
      UWHUYCSUXNUXLUXOUWMSUXDUXNUWMVHVJUYAUXEUWHUWMUWSUYAUXEUWDUWHUWMUWSSZSUXDU
      XLUXEUWDSUXNUXDUWDVHVKUWHUWDUYDUWHUCNZUVMOZUCGQUWDUYDSZUWGUYFDUCGUULUYEUV
      MTVMUYFUYGUCGUWMUYFUWDUYEGMZUWSUWMLNZUVSOZLGQUYFUWDUYHUWSSSSZUWLUYJDLGUUL
      UYIUVSTVMUYJUYKLGUYIGMZUYHUYFUWDUYJUWSUYLUYHUWDUYFUYJUWSSZUYLUYHUWDUYFUYM
      SZSUYLUYHPUWDUUNUYIUYEUEZOZEGQZUYNUURUYQUUNUYIUUOUEZOZEGQDCUYIUYEGGUULUYI
      VNZUUQUYSEGUYTUUPUYRUUNUULUYIUUOVOVPVQUUOUYEVNZUYSUYPEGVUAUYRUYOUUNUUOUYE
      UYIVRVPVQVSUYPUYNEGUUNGMZUYPUYFUYJUWSUYPUYFUYJPPZVUBUUNUVTOZUWSVUCUUNUVMO
      ZUUNUVSOZPVUDUYPUYFVUEUYJVUFUYPUUNUYEOZUYFVUEUYPUYOUYEOVUGUYIUYEVDUUNUYOU
      YEWCVTUUNUYEUVMWCWAUYPUUNUYIOZUYJVUFUYPUYOUYIOVUHUYIUYEVBUUNUYOUYIWCVTUUN
      UYIUVSWCWAWBUUNUVMUVSWDWEUWRVUDDUUNGUULUUNUVTTWFWOWGWHWIWPWJWKWHWLWMWHWLW
      NWIWQXFWRXFWSWIWTXGXAXBXCXDUWDAJUPUWSAUVTUPXEXHWNXIUXBUWJUXCUWOUVJUWJHUVM
      UAXPZUVEUVMVNZUVFUWFUVIUWIUVEUVMJTUVHUWHAUVEUVMVUJUVGUWGDGUVEUVMUULXJVQXK
      XLXQUVJUWOHUVSUBXPUVEUVSVNZUVFUWKUVIUWNUVEUVSJTUVHUWMAUVEUVSVUKUVGUWLDGUV
      EUVSUULXJVQXKXLXQUQUVJUXAHUVTUVMUVSVUIXMUVEUVTVNZUVFUWQUVIUWTUVEUVTJTUVHU
      WSAUVEUVTVULUVGUWRDGUVEUVTUULXJVQXKXLXQXEXNUVQUAUVNUVOJOZUULUVOOZDGQZAUVO
      RZUVPUVNUYIJOZLUVMRVUMUVNVUQLUVMUVNUYIUVMMZUYIUVKMZVUQUVMUVKUYIVEZVUSVUQU
      ULUYIOZDGQZAUYIRZUVJVUQVVCPZHUYILXPUVEUYIVNZUVFVUQUVIVVCUVEUYIJTUVHVVBAUV
      EUYIVVEUVGVVADGUVEUYIUULXJVQXKXLXQZXOWIXRLUVMJXSYIUVNVUOAUVOALUVMUVKVURAX
      TUVJAHLUVFUVIAUVFAXTUVHAUVEYAYBYCYDUUKUVOMUUKUYIMZVURPZLYEUVNVUOLUUKUVMYF
      UVNVVHVUOLUVNVVGVURVUOVURUVNVVGVUOVURUVNVUSVVGVUOSZUVNVURVUSVUTWNVUSVVDVU
      RVVIVVFVURVVCVVIVUQVVCVVGVVBVURVUOVVBAUYIYGVURVVAVUNDGVURUYIUVOOVVAVUNUYI
      UVMYHUULUYIUVOYJVFYKYLYMYNXFWTYOYPYNYQUVJVUMVUPPHUVOUVMVUIYRUVEUVOVNZUVFV
      UMUVIVUPUVEUVOJTUVHVUOAUVEUVOVVJUVGVUNDGUVEUVOUULXJVQXKXLXQUUAUUBUUCYSULU
      UIUVLUWCUUDZUVDUUIUVKYTMVVKUVIHJFUUEUAUBYTUVKUUFYSUUGUUH $.
      $( [8-Sep-2009] $)

    ${
      neibastop2.1 $e |- G = { <. f , h >. | h = { y | ( y e. X /\ E. w e. W w
      C_ f ) } } $.
      neibastop2.2 $e |- F = { <. a , b >. | b = { c | E. z e. a E.
      x e. ( G ` z ) ( c e. U /\ c C_ z ) } } $.
      $( Lemma for ~ neibastop2 . $)
      neibastop2lem1 $p |- ( k e. om -> ( ( ( X e. B /\ A. x e. X A. v e. U ( x
    e. v /\ v C_ X ) ) /\ ( u e. U /\ u C_ n ) ) -> U. ( rec ( F , { u } ) ` k
    ) C_ n ) ) $=
        ( vj vm vr vs vd wcel cv wss wa wral csn crdg cfv cuni wi c0 csuc fveq2
        wceq unieqd sseq1d imbi2d snex rdg0 unieqi vex unisn eqtri syl5eqss com
        simprr con0 nnon rdgsuc syl adantr wrex cab wfun copab ax-17 hbre1 hbab
        cvv hbeq hbopab hbxfreq hbrdg hbfv sseq2 anbi2d rexeqbidv cbvrexf abbii
        hbab1 hbrex eleq1 sseq1 anbi12d 2rexbidv cbvab fvex df-pw pwex eqeltrri
        cop cpw simpr ss2abi ssexi ab2rexex2 eqeltri a1i funopabeq funeqi mpbir
        jctir rexeqf abbid eqeq2d eqeq1 opelopab rexeq abbidv sylan9bb cbvopabv
        eqid eqtr4i eleq2i bitr3i mpbi funopfvg sylc eluni hban syl5bi eqsstrd
        wex hblem elabf anbi2i hbra1 hbuni hbim elunii expcom syl9r adantld a1d
        ssel rexlimd rexlimdva com23 imp3a exlimdv ssrdv ad2antrl ex sstr2 syl6
        adantl a2d finds ) PGUHZAUIZEUIZUHUVHPUJUKEHULZAPULZUKZFUIZHUHZUVLLUIZU
        JZUKZUKZUCUIZMUVLUMZUNZUOZUPZUVNUJZUQUVQURUVTUOZUPZUVNUJZUQUVQUDUIZUVTU
        OZUPZUVNUJZUQUVQUWGUSZUVTUOZUPZUVNUJZUQUVQKUIZUVTUOZUPZUVNUJZUQUCUDUWOU
        VRURVAZUWCUWFUVQUWSUWBUWEUVNUWSUWAUWDUVRURUVTUTVBVCVDUVRUWGVAZUWCUWJUVQ
        UWTUWBUWIUVNUWTUWAUWHUVRUWGUVTUTVBVCVDUVRUWKVAZUWCUWNUVQUXAUWBUWMUVNUXA
        UWAUWLUVRUWKUVTUTVBVCVDUVRUWOVAZUWCUWRUVQUXBUWBUWQUVNUXBUWAUWPUVRUWOUVT
        UTVBVCVDUVQUWEUVLUVNUWEUVSUPUVLUWDUVSUVSMUVLVEVFVGUVLFVHVIVJUVKUVMUVOVM
        VKUWGVLUHZUVQUWJUWNUXCUVQUWMUWIUJZUWJUWNUQUXCUVQUXDUXCUVQUKUWMUWHMUOZUP
        ZUWIUXCUWMUXFVAUVQUXCUWLUXEUXCUWGVNUHUWLUXEVAUWGVOUVSUWGMVPVQVBVRUVKUXF
        UWIUJUXCUVPUVKUXFSUIZHUHZUXGCUIZUJZUKZAUXINUOZVSZCUWHVSZSVTZUPZUWIUVKUX
        EUXOUVKUXOWFUHZMWAZUKUWHUXOXHZMUHZUXEUXOVAUVKUXQUXRUXQUVKUXOUEUIZHUHZUY
        AUFUIZUJZUKZAUYCNUOZVSZUFUWHVSZUEVTZWFUXOUXHUXGUYCUJZUKZAUYFVSZUFUWHVSZ
        SVTUYIUXNUYMSUXMUYLCUFUCUWHCUCUWGUVTCUCUVSMCUCMRUIZUXMCQUIZVSZSVTZVAZQR
        WBZUBUYRQRCUCCUCUCUYNUYQUVRUYNUHZCWCUYPCSUCUXMCUYOWDZWEWGWHWIUVRUVSUHZC
        WCWJUVRUWGUHZCWCWKZUVRUWHUHUFWCUXMUFWCUYLCWCUXIUYCVAZUXKUYKAUXLUYFUXIUY
        CNUTVUEUXJUYJUXHUXIUYCUXGWLWMWNWOWPUYMUYHSUEUYMUEWCUYGSUFUWHSUFUWGUVTSU
        FUVSMSUFMUYSUBUYRQRSUFSUCUCUYNUYQUYTSWCUYPSUCWQWGZWHWIUYCUVSUHSWCWJUYCU
        WGUHSWCWKUYGSWCWRUXGUYAVAZUYKUYEUFAUWHUYFVUGUXHUYBUYJUYDUXGUYAHWSUXGUYA
        UYCWTXAXBXCVJUYEUFAUEUWHUYFUWGUVTXDZUYCNXDUYEUEVTUYDUEVTZUYCXIVUIWFUEUY
        CXEUYCUFVHXFXGUYEUYDUEUYBUYDXJXKXLXMXNZXOUXRUYSWAQRUYQXPMUYSUBXQXRXSUXT
        UVKUXOUXOVAZUXTUXOYIVUKUXSUYCUXMCUYAVSZSVTZVAZUEUFWBZUHUXTVUNUYCUXOVAVU
        KUEUFUWHUXOVUHVUJUYAUWHVAZVUMUXOUYCVUPVULUXNSSUCUCUYAUWHUVRUYAUHZSWCSUC
        UWGUVTSUCUVSMSUCMUYSUBUYRQRSUCVUFWHWIVUBSWCWJVUCSWCWKZWGUXMCUCUYAUWHVUQ
        CWCVUDXTYAYBUYCUXOUXOYCYDVUOMUXSVUOUYSMVUNUYRUEUFQRUYAUYOVAZVUNUYCUYQVA
        UYCUYNVAUYRVUSVUMUYQUYCVUSVULUYPSUXMCUYAUYOYEYFYBUYCUYNUYQYCYGYHUBYJYKY
        LYMXOUWHUXOWFMYNYOVBUVKUCUXPUWIUVRUXPUHUVRUVNUHZUVNUXOUHZUKZLYTUVKUVRUW
        IUHZLUVRUXOYPUVKVVBVVCLVVBVUTUVNHUHZUVNUXIUJZUKZAUXLVSZCUWHVSZUKUVKVVCV
        VAVVHVUTUXNVVHSUVNVVGSCUWHSUCCUWHVURUUAVVGSWCWRLVHUXGUVNVAZUXKVVFCAUWHU
        XLVVIUXHVVDUXJVVEUXGUVNHWSUXGUVNUXIWTXAXBUUBUUCUVKVUTVVHVVCUVKVVHVUTVVC
        VVHVVDUVNUWOUJZUKZAUWONUOZVSZKUWHVSUVKVUTVVCUQZVVGVVMCKUGUWHCUGUWGUVTCU
        GUVSMCUGMUYSUBUYRQRCUGCUGUGUYNUYQUGUIZUYNUHCWCUYPCSUGVUAWEWGWHWIVVOUVSU
        HCWCWJVVOUWGUHCWCWKVVOUWHUHKWCVVGKWCVVMCWCUXIUWOVAZVVFVVKAUXLVVLUXIUWON
        UTVVPVVEVVJVVDUXIUWOUVNWLWMWNWOUVKVVMVVNKUWHUVKUWOUWHUHZUKVVKVVNAVVLUVK
        VVQAUVFUVJAUVFAWCUVIAPUUDYQAKUWGUVTAKUVSMAKMUYSUBUYRQRAKAKKUYNUYQUWOUYN
        UHAWCUYPASKUXMACUYOUXIUYOUHAWCUXKAUXLWDWRWEWGZWHWIUWOUVSUHAWCWJUWOUWGUH
        AWCWKYQVUTVVCAVUTAWCAUCUWHAUCUWGUVTAUCUVSMAUCMUYSUBUYRQRAUCVVRWHWIVUBAW
        CWJVUCAWCWKUUEUUFVVQUVGVVLUHZVVKVVNUQZUQUVKVVQVVTVVSVVQVVJVVNVVDVVJVUTU
        VRUWOUHZVVQVVCUVNUWOUVRUULVWAVVQVVCUVRUWOUWHUUGUUHUUIUUJUUKUVCUUMUUNYRU
        UOUUPYRUUQYRUURYSUUSYSUUTUWMUWIUVNUVAUVBUVDUVE $.
        $( [18-Sep-2009] $)

      $( Lemma for ~ neibastop2 . $)
      neibastop2lem2 $p |- ( ( ( X e. B /\ A. x e. X ( U =/= (/) /\ A. v e. U (
          (
    x e. v /\ v C_ X ) /\ ( A. w e. U E. t e. U t C_ ( v i^i w ) /\ E. t e. U
    A. y e. t E. w e. W w C_ v ) ) ) ) /\ ( ( ( { o | ( o C_ X /\ A. x e. o E.
    v e. U v C_ o ) } e. Top /\ x e. X ) /\ n C_ X ) /\ ( u e. U /\ u C_ n ) )
    ) -> U_ k e. om U. ( G " ( rec ( F , { u } ) ` k ) ) C_ n ) $=
        ( vd vm vj wcel c0 wne wss cin wrex wral cab ctop com csn crdg cfv cima
        cv wa cuni ciun wceq wex eluni cop vex elima3 copab eleq2i sseq2 anbi2d
        rexbidv abbidv eqeq2d eqeq1 opelopab bitri anbi2i exbii wi simprr ax-17
        csb hbcsb1 hbrex hban eleq1 csbeq1a rexeqdv anbi12d elabf simpll ralimi
        adantl hbral raleqbidv rcla4 sseq1 cbvrexv eqcomd eqcoms cbvcsbv rcla4v
        cvv ax-mp eleq2 ssel com12 a1i34 syl6com rexlimdv syl imp3a com24 imp31
        syl5bi adantlll ssrdv adantrr eqsstrd simprl jctird elunii syl6 exlimdv
        sseld ex com23 ralrimivw ss2iun neibastop2lem1 adantrl sylanl2 ralrimiv
        simpl iunss sylibr sstrd ) RHUHZIUIUJZAVBZEVBZUHZUUFRUKZVCZGVBZUUFDVBZU
        LUKGIUMDIUNUUKUUFUKDQUMBUUJUNGIUMVCZVCZEIUNZVCZARUNZVCNVBZRUKUUFUUQUKEI
        UMAUUQUNVCNUOUPUHUUERUHVCMVBZRUKVCZFVBZIUHUUTUURUKVCZVCZVCZLUQPLVBZOUUT
        URUSUTZVAZVDZVEZLUQUVEVDZVEZUURUVCUVGUVIUKZLUQUNUVHUVJUKUVCUVKLUQUVCUEU
        VGUVIUEVBZUVGUHZUVLUFVBZUHZUGVBZUVEUHZUVNBVBZRUHZUUKUVPUKZDQUMZVCZBUOZV
        FZVCZUGVGZVCZUFVGZUVCUVLUVIUHZUVMUVOUVNUVFUHZVCZUFVGUWHUFUVLUVFVHUWKUWG
        UFUWJUWFUVOUWJUVQUVPUVNVIZPUHZVCZUGVGUWFUGUVNPUVEUFVJZVKUWNUWEUGUWMUWDU
        VQUWMUWLKVBZUVSUUKJVBZUKZDQUMZVCZBUOZVFZJKVLZUHUWDPUXCUWLUCVMUXBUWPUWCV
        FUWDJKUVPUVNUGVJUWOUWQUVPVFZUXAUWCUWPUXDUWTUWBBUXDUWSUWAUVSUXDUWRUVTDQU
        WQUVPUUKVNVPVOVQVRUWPUVNUWCVSVTWAWBWCWAWBWCWAUVCUWGUWIUFUVCUVOUWFUWIUVC
        UWFUVOUWIUVCUWEUVOUWIWDZUGUVCUWEUXEUVCUWEVCZUVOUVLUVPUHZUVQVCUWIUXFUVOU
        XGUVQUXFUVNUVPUVLUXFUVNUWCUVPUVCUVQUWDWEUVCUVQUWCUVPUKUWDUVCUVQVCZUFUWC
        UVPUVNUWCUHUVNRUHZUVTDBUVNQWGZUMZVCZUXHUVNUVPUHZUWBUXLBUVNUXIUXKBUXIBWF
        UVTBDUXJBDUVNQUWOUUKUVNUHBWFWHUVTBWFWIWJUWOUVRUVNVFZUVSUXIUWAUXKUVRUVNR
        WKUXNUVTDQUXJBUVNQWLWMWNWOUUPUVBUVQUXLUXMWDZUUCUUPUVBUVQUXOUUPUXLUVQUVB
        UXMUUPUXIUXKUVQUVBUXMWDWDZUUPUUGEIUNZARUNZUXIUXKUXPWDZWDUUOUXQARUUNUXQU
        UDUUMUUGEIUUGUUHUULWPWQWRWQUXIUXRUVNUUFUHZEAUVNIWGZUNZUXSUXQUYBAUVNRUXT
        AEUYAAEUVNIUWOUUFUVNUHAWFWHUXTAWFWSUUEUVNVFUUGUXTEIUYAAUVNIWLUUEUVNUUFW
        KWTXAUXKUVLUVPUKZUEUXJUMUYBUXPUVTUYCDUEUXJUUKUVLUVPXBXCUYBUYCUXPUEUXJUV
        LUXJUHUVLUYAUHZUYBUYCUXPWDZUXJUYAUVLUVNXHUHUXJUYAVFUWOBAUVNQIXHQIVFUUEU
        VRUUEUVRVFIQUBXDXEXFXIVMUYDUYBUVNUVLUHZUYEUXTUYFEUVLUYAUUFUVLUVNXJXGUYF
        UYCUVQUVBUXMUYCUYFUXMUVLUVPUVNXKXLXMXNXTXOXTXNXPXQXRXSYAXTYBYCYDYJUVCUV
        QUWDYEYFUVLUVPUVEYGYHYKYIYLXQYIXTYBYMLUQUVGUVIYNXPUVCUVIUURUKZLUQUNUVJU
        URUKUVCUYGLUQUUPUUCUUIEIUNZARUNZUVBUVDUQUHZUYGWDZUUOUYHARUUNUYHUUDUUMUU
        IEIUUIUULYSWQWRWQUUCUYIVCZUVAUYKUUSUYJUYLUVAVCUYGABCDEFHIJKLMOPQRSTUAUB
        UCUDYOXLYPYQYRLUQUVIUURYTUUAUUB $.
        $( [15-Sep-2009] $)

      $( Lemma for ~ neibastop2 . $)
      neibastop2lem3 $p |- ( ( ( X e. B /\ A. x e. X ( U =/= (/) /\ A. v e. U (
          (
    x e. v /\ v C_ X ) /\ ( A. w e. U E. t e. U t C_ ( v i^i w ) /\ E. t e. U
    A. y e. t E. w e. W w C_ v ) ) ) ) /\ ( ( ( m e. U /\ m C_ d ) /\ ( x e. X
    /\ d e. ( rec ( F , { u } ) ` j ) ) ) /\ j e. om ) ) -> E. v e. U v C_ U. (
    G " { c | E. z e. ( rec ( F , { u } ) ` j ) E. x e. ( G ` z ) ( c e. U /\ c
    C_ z ) } ) ) $=
        ( vn vr vk wcel c0 wne cv wss wa cin wrex wral csn crdg cfv com cab wex
        cima wi wal cuni ra4 simplr ralimi sseq1 cbvralv sylib id adantrl eleq2
        wceq anbi12d sseq2 rexbidv rexralbidv jca com12 ax-17 hbra1 simplll csb
        hbim ssel2 adantll ad2ant2rl hbcsb1 hbrex csbeq1a rexeqdv rcla4 adantlr
        vex impcom eleq1 elabf sylanbrc cvv wfun cop df-rab syl5eqelr ad3antrrr
        crab rabexg ad2antrl copab jctir eqid eleq2i anbi2d abbidv eqeq2d eqeq1
        opelopabg sylancr syl5bb mpbiri funopfvg sylc eleqtrrd ad2antlr rcla4ev
        wb syl2anc sylibr fveq2 rexeqbidv hbeq hbopab hbxfreq hbrdg com23 imp3a
        hbfv syld ad2ant2l hban funopabeq funeqi mpbir cdm ralrimivw eleq1d wfn
        biantrur opabbii eqtri fnopab2g df-fn bitri anim2i sylbi ancoms simplrr
        syl simpllr cbvrexv eqcomd eqcoms elab 19.8a syl12anc df-rex hbre1 hbab
        cbvrexf hbab1 hblem 2rexbidv funfvima fvex cla4ev alrimiv imbi1d albidv
        expr exp58 com25 rexlimd com14 syl5 imp4a com34 syl9r imp4b imp44 dfss2
        exp510 adantld eluni imbi2i albii bitr2i rexbii ) QHUHZIUIUJZAUKZEUKZUH
        ZUXCQULZUMZGUKZUXCDUKZUNULGIUODIUPZUXHUXCULZDPUOZBUXGUPGIUOZUMZUMZEIUPZ
        UMZAQUPZUMZMUKZIUHZUXSUAUKZULZUMZUXBQUHZUYALUKZNFUKUQZURZUSZUHZUMZUMUYE
        UTUHZUMUMUEUKZUXCUHZUYLUFUKZUHZUYNOTUKZIUHZUYPCUKZULZUMZAUYROUSZUOZCUYH
        UOZTVAZVCZUHZUMZUFVBZVDZUEVEZEIUOZUXCVUEVFZULZEIUOUXRUYCUYJUYKVUKUXRUYJ
        UYCUYKVUKVDZUWTUXQUYDUYIUYCVUNVDZUXQUYDUXPUWTUYIVUOVDZUXPAQVGUWTUXOVUPU
        XAUWTUXOUYCUYIVUNUWTUXOUXTUYBUYIVUNVDZUXOUXGQULZGIUPZUXBUXSUHZUXSQULZUM
        ZUXHUXSULZDPUOZBUXGUPZGIUOZUMZMIUPZUMUWTUXTUYBVUQVDZVDZUXOVUSVVHUXOUXEE
        IUPVUSUXNUXEEIUXDUXEUXMVHVIUXEVUREGIUXCUXGQVJVKVLUXOUXFUXLUMZEIUPVVHUXN
        VVKEIUXFUXLVVKUXIVVKVMVNVIVVKVVGEMIUXCUXSVPZUXFVVBUXLVVFVVLUXDVUTUXEVVA
        UXCUXSUXBVOUXCUXSQVJVQVVLUXKVVDGBIUXGVVLUXJVVCDPUXCUXSUXHVRVSVTVQVKVLWA
        UWTVUSVVHVVJUXTVUSVVHUWTVVIUXTVVHVUSUWTVVIVDZUXTVVHVVGVUSVVMVDZVVHUXTVV
        GVVGMIVGWBUXTVVBVVFVVNUXTVVFVVBVVNUXTVVEVVBVVNVDZGIUXTGWCVVBVVNGVVBGWCV
        USVVMGVURGIWDVVMGWCWGWGUXGIUHZUXTVVEVVOVDVVPVUSVVEVVBUXTVVMVVPVUSVURVVE
        VVBUXTVVMVDVDVDVUSVVPVURVURGIVGWBVVPVURVVEVVBUXTVVMVVPVURUMZVVEVVBUMZUX
        TUMZUMZUWTUYBUYIUYKVUKVVTUWTUYBUMZUYIUMUYKUMZUMZVVPUYLUXGUHZVUHVDZUEVEZ
        VUKVVPVURVVSVWBWEVWCVWEUEVVTVWBVWDVUHVVTVWBVWDUMZUMZUYLUXSOUSZUHZVWIVUE
        UHZVUHVWHUYLBUKZQUHZVVDUMZBVAZVWIVWHUYLQUHZVVCDBUYLPWFZUOZUYLVWOUHVVQVW
        DVWPVVSVWBVURVWDVWPVVPUXGQUYLWHWIWJVVSVWDVWRVVQVWBVVRVWDVWRUXTVVEVWDVWR
        VVBVWDVVEVWRVVDVWRBUYLUXGVVCBDVWQBDUYLPUEWQZUXHUYLUHBWCWKVVCBWCWLZVWLUY
        LVPZVVCDPVWQBUYLPWMWNZWOWRWPWPUUAVWNVWPVWRUMBUYLVWPVWRBVWPBWCVWTUUBVWSV
        XAVWMVWPVVDVWRVWLUYLQWSVXBVQWTXAVWHVWOXBUHZOXCZUMUXSVWOXDZOUHZVWIVWOVPV
        WHVXCVXDVWBVXCVVTVWDUWTVXCUYBUYIUYKUWTVWOVVDBQXHXBVVDBQXEVVDBQHXIXFZXGX
        JVXDKUKZVWMUXHJUKZULZDPUOZUMZBVAZVPZJKXKZXCJKVXMUUCOVXOUCUUDUUEZXLVWBVX
        FVVTVWDUWTVXFUYBUYIUYKUWTVXFVWOVWOVPZVWOXMVXFVXEVXOUHZUWTVXQOVXOVXEUCXN
        UWTUXSXBUHZVXCVXRVXQYHMWQZVXGVXNVXHVWOVPVXQJKUXSVWOXBXBVXIUXSVPZVXMVWOV
        XHVYAVXLVWNBVYAVXKVVDVWMVYAVXJVVCDPVXIUXSUXHVRVSXOXPXQVXHVWOVWOXRXSXTYA
        YBXGXJUXSVWOXBOYCYDYEVWHVXDUXSOUUFZUHZUMZUXSVUDUHZVWKVWHVXMXBUHZJXBUPZV
        YDVWHVWMUXHUGUKZULZDPUOZUMZBVAZXBUHZUGXBUPVYGVWHVYMUGXBVWBVYMVVTVWDUWTV
        YMUYBUYIUYKUWTVYLVYJBQXHXBVYJBQXEVYJBQHXIXFXGXJUUGVYMVYFUGJXBVYHVXIVPZV
        YLVXMXBVYNVYKVXLBVYNVYJVXKVWMVYNVYIVXJDPVYHVXIUXHVRVSXOXPUUHVKVLVYGVXDV
        YBXBVPZUMZVYDVYGOXBUUIVYPJKXBVXMOOVXOVXIXBUHZVXNUMZJKXKUCVXNVYRJKVYQVXN
        JWQUUJUUKUULUUMOXBUUNUUOVYOVYCVXDVYOVYCVXSVXTVYBXBUXSVOYBUUPUUQUUTVWHUX
        TUXSUYRULZUMZAVUAUOZCUYHUOZVYEVWHUXTUXSVYHULZUMZAVYHOUSZUOZUGUYHUOZWUBV
        WHUYIUYCAUYAOUSZUOZWUGVWBUYIVVTVWDVWAUYIUYKVHXJVWHUXBWUHUHZUYCUMZAVBZWU
        IVWHWUJUXTUYBWULVWHUXBVWMUXHUYAULZDPUOZUMZBVAZWUHVWHUYDWUMDIUOZUXBWUPUH
        VVSUYDVVQVWGVVBUYDVVEUXTVVAVUTUYDUXSQUXBWHUURYFYFVWHVYHUYAULZUGIUOZWUQV
        WHUXTUYBWUSVVQVVRUXTVWGUUSZVWBUYBVVTVWDUWTUYBUYIUYKUVAXJZWURUYBUGUXSIVY
        HUXSUYAVJYGYIWURWUMUGDIVYHUXHUYAVJUVBVLWUOUYDWUQUMBUXBAWQVWLUXBVPZVWMUY
        DWUNWUQVWLUXBQWSWVBWUMDPIPIVPUXBVWLUXBVWLVPIPUBUVCUVDWNVQUVEXAVWHWUPXBU
        HZVXDUMUYAWUPXDZOUHZWUHWUPVPVWHWVCVXDVWBWVCVVTVWDUWTWVCUYBUYIUYKUWTWUPW
        UNBQXHXBWUNBQXEWUNBQHXIXFZXGXJVXPXLVWBWVEVVTVWDUWTWVEUYBUYIUYKUWTWVEWUP
        WUPVPZWUPXMWVEWVDVXOUHZUWTWVGOVXOWVDUCXNUWTUYAXBUHWVCWVHWVGYHUAWQWVFVXN
        VXHWUPVPWVGJKUYAWUPXBXBVXIUYAVPZVXMWUPVXHWVIVXLWUOBWVIVXKWUNVWMWVIVXJWU
        MDPVXIUYAUXHVRVSXOXPXQVXHWUPWUPXRXSXTYAYBXGXJUYAWUPXBOYCYDYEWUTWVAWUKAU
        VFUVGUYCAWUHUVHYJWUFWUIUGUYAUYHVYHUYAVPZWUDUYCAWUEWUHVYHUYAOYKWVJWUCUYB
        UXTVYHUYAUXSVRXOYLYGYIWUFWUAUGCMUYHUXSUYHUHUGWCCMUYEUYGCMUYFNCMNSUKZVUB
        CRUKZUOZTVAZVPZRSXKZUDWVORSCMCMMWVKWVNUXSWVKUHZCWCWVMCTMVUBCWVLUVIUVJYM
        YNYOUXSUYFUHZCWCYPUXSUYEUHZCWCYSWUFCWCWUAUGWCVYHUYRVPZWUDVYTAWUEVUAVYHU
        YROYKWVTWUCVYSUXTVYHUYRUXSVRXOYLUVKVLVUCWUBTUXSWUATCUYHTMCUYHTMUYEUYGTM
        UYFNTMNWVPUDWVORSTMTMMWVKWVNWVQTWCWVMTMUVLYMYNYOWVRTWCYPWVSTWCYSUVMWUAT
        WCWLVXTUYPUXSVPZUYTVYTCAUYHVUAWWAUYQUXTUYSVYSUYPUXSIWSUYPUXSUYRVJVQUVNW
        TYJVUDUXSOUVOYDVUGVWJVWKUMUFVWIUXSOUVPUYNVWIVPUYOVWJVUFVWKUYNVWIUYLVOUY
        NVWIVUEWSVQUVQYIUWAUVRVUJVWFEUXGIUXCUXGVPZVUIVWEUEWWBUYMVWDVUHUXCUXGUYL
        VOUVSUVTYGYIUWMUWBYTUWCWBUWDYQYRYTYQUWEYRUWFUWGUWHUWNUWIUWJYQUWKVUJVUME
        IVUMUYMUYLVULUHZVDZUEVEVUJUEUXCVULUWLWWDVUIUEWWCVUHUYMUFUYLVUEUWOUWPUWQ
        UWRUWSVL $.
        $( [19-Sep-2009] $)

      $( Lemma for ~ neibastop2 . $)
      neibastop2lem4 $p |- ( ( ( X e. B /\ A. x e. X ( U =/= (/) /\ A. v e. U (
          (
    x e. v /\ v C_ X ) /\ ( A. w e. U E. t e. U t C_ ( v i^i w ) /\ E. t e. U
    A. y e. t E. w e. W w C_ v ) ) ) ) /\ ( ( { o | ( o C_ X /\ A. x e. o E. v
    e. U v C_ o ) } e. Top /\ x e. X ) /\ ( n C_ X /\ E. u e. U u C_ n ) ) ) ->
    E. g e. { o | ( o C_ X /\ A. x e. o E. v e. U v C_ o ) } ( x e. g /\ g C_ n
    ) ) $=
        ( vk vj vm vd vs vr wcel c0 wne cv wss wa cin wrex wral cab ctop wi com
        csn crdg cima cuni ciun ax-17 eliun fveq2 imaeq2d unieqd eleq2d cbvrexv
        cfv wceq bitri cop wex anbi2i copab eleq2i rexbidv anbi2d abbidv eqeq2d
        sseq2 eqeq1 opelopab eleq1 rexeqdv anbi12d elab ad2antll sseq1 rexlimdv
        vex syl5bi fveq1i imaeq2i unieqi sseq2i rexbii wfun funopabeq eqid fvex
        cvv abbii eqtri opabbii ax-mp hbre1 hbab hbeq hbopab hbxfreq hbrdg hbfv
        wb rexeqf mpbir sylib sseq2d rcla4ev eleq2 com23 imp3a exlimdv ad2antrr
        syl crab df-rab rabexg syl5eqelr sylancr sylibr hbrex expr simprlr sstr
        neibastop2lem2 ancoms sylan hbra1 hban eluni elima3 exbii eqcomd eqcoms
        csuc peano2 neibastop2lem3 exp44 exp5c com24 sylan9bb cbvopabv 2rexbidv
        imp511 rexeq cbvabv rexeqbidv eqeq2i rdgeq1 cpw df-pw pwex simpr ss2abi
        eqeltrri ssexi ab2rexex2 eqeltri hbab1 abbid funopfv bitr2i con0 rdgsuc
        mp2 nnon mpbird syl2anc ex rexcom syl6ib ssiun reximi syl6 exp4c syl7bi
        imbi1d imbi2d syl5ibrcom ralrimi peano1 simplr ad2antrl adantr sylanbrc
        ssid mpan2 funeqi jctir opelopabg syl5bb mpbiri funopfvg eleqtrrd unisn
        sylc ralrimivw biantrur fnopab2g sylancl syl5eqr eleqtrd snex syl6eleqr
        wfn fnsnfv rdg0 anim1i jca31 mpdan omex uniex iunex hbima hbuni hbxfrbi
        funimaex raleqf ralbid bitrd anbi1i exp3a impr ) RHUKZIULUMAUNZEUNZUKVU
        DRUOUPGUNZVUDDUNZUQUOGIURDIUSVUFVUDUODQURBVUEUSGIURUPUPEIUSUPZARUSZUPZN
        UNZRUOZVUDVUJUOZEIURZAVUJUSZUPZNUTZVAUKZVUCRUKZUPZMUNZRUOZFUNZVUTUOZFIU
        RZUPVUCKUNZUKZVVEVUTUOZUPZKVUPURZVUIVUSUPVVAVVDVVIVUIVUSVVAVVDVVIVBVUIV
        USVVAUPZUPZVVCVVIFIVVKVVBIUKZVVCVVIVUIVVJVVLVVCUPZVVIVUIVVJVVMUPZUPZUEV
        CPUEUNZOVVBVDZVEZVPZVFZVGZVHZVUPUKZVUCVWBUKZVWBVUTUOZUPZUPZVVIVVOVWBRUO
        ZVUDVWBUOZEIURZAVWBUSZUPZVWFUPZVWGVVOVWEVWMABCDEFGHIJLUEMNOPQRSTUAUBUCU
        DUUCVVOVWEUPVWHVWKVWFVVOVVAVWEVWHVUIVUSVVAVVMUUAVWEVVAVWHVWBVUTRUUBUUDU
        UEVUIVWKVVNVWEVUIVWJAVWBVUBVUHAVUBAVIVUGARUUFUUGVWDVUCPUFUNZVVRVPZVFZVG
        ZUKZUFVCURZVUIVWJVWDVUCVWAUKZUEVCURZVWSUEVUCVCVWAVJZVWTVWRUEUFVCVVPVWNV
        QZVWAVWQVUCVXCVVTVWPVXCVVSVWOPVVPVWNVVRVKVLVMVNVOVRVUIVWRVWJUFVCVUIVWRV
        WNVCUKZVWJVWRVUCUGUNZUKZUHUNZVWOUKZVXGVXEVSZPUKZUPZUHVTZUPZUGVTZVUIVXDV
        WJVBZVWRVXFVXEVWPUKZUPZUGVTVXNUGVUCVWPUUHVXQVXMUGVXPVXLVXFUHVXEPVWOUGWR
        ZUUIWAUUJVRVUIVXMVXOUGVUIVXFVXLVXOVUIVXLVXFVXOVUIVXKVXFVXOVBZUHVXKVXHVX
        EBUNZRUKZVUFVXGUOZDQURZUPZBUTZVQZUPVUIVXSVXJVYFVXHVXJVXILUNZVYAVUFJUNZU
        OZDQURZUPZBUTZVQZJLWBZUKVYFPVYNVXIUCWCVYMVYGVYEVQVYFJLVXGVXEUHWRVXRVYHV
        XGVQZVYLVYEVYGVYOVYKVYDBVYOVYJVYCVYAVYOVYIVYBDQVYHVXGVUFWHWDWEWFWGVYGVX
        EVYEWIWJVRWAVUIVXHVYFVXSVUIVYFVXHVXSVUIVXHVXSVBVYFVXHVUCVYEUKZVXOVBZVBV
        YPVURVYBDIURZUPZVUIVXHVXOVYDVYSBVUCAWRZVXTVUCVQZVYAVURVYCVYRVXTVUCRWKZW
        UAVYBDQIQIVQVUCVXTVUCVXTVQIQUBUUKUULZWLWMWNVUIVXHVYSVXDVWJVUIVXHVYSUPZV
        XDUPZVUDVWAUOZUEVCURZEIURZVWJVUIWUEWUFEIURZUEVCURZWUHVUIWUEWUJVUIWUEUPZ
        VWNUUMZVCUKZVUDPWULVVRVPZVFZVGZUOZEIURZWUJVXDWUMVUIWUDVWNUUNWOWUKWURVUD
        PVWOOVPZVFZVGZUOZEIURZWUKVUDPUAUNZIUKZWVDCUNZUOZUPZAWVFPVPZURZCVWOURZUA
        UTZVFZVGZUOZEIURZWVCVUIVXHVURVYRVXDWVPVUIVYRVURVXHVXDWVPVBZVYRVXEVXGUOZ
        UGIURVUIVURVXHWVQVBVBZVYBWVRDUGIVUFVXEVXGWPVOVUIWVRWVSUGIVUIVXEIUKZWVRV
        URVXHWVQVUIWVTWVRUPVURVXHUPVXDWVPABCDEFGHIJLUFUGOPQRSTUAUHUBUCUDUUOUUPU
        UQWQWSUURUVBWVCVUDPVWOTUNZWVJCSUNZURZUAUTZVQZSTWBZVPZVFZVGZUOZEIURWVPWV
        BWWJEIWVAWWIVUDWUTWWHWUSWWGPVWOOWWFUDWTXAXBXCXDWWJWVOEIWWIWVNVUDWWHWVMW
        WGWVLPWWFXEVWOWVLVSZWWFUKZWWGWVLVQSTWWDXFWWLWVLWVLVQZWVLXGWWLWWKUIUNZWV
        JCUJUNZURZUAUTZVQZUJUIWBZUKWWMWWFWWSWWKWWEWWRSTUJUIWWBWWOVQZWWEWWAWWQVQ
        WWAWWNVQWWRWWTWWDWWQWWAWWTWWCWWPUAWVJCWWBWWOUVCWFWGWWAWWNWWQWIUUSUUTWCW
        WRWWNWVLVQWWMUJUIVWOWVLVWNVVRXHWVLWVJCVWNWWAWWOIUKZWWOWWNUOZUPZAWWNPVPZ
        URZUIWWBURZUJUTZVQZSTWBZVVQVEZVPZURZUAUTXIWVKWXLUAVWOWXKVQWVKWXLYAVWNVV
        RWXJOWXIVQVVRWXJVQOWWFWXIUDWWEWXHSTWWDWXGWWAWWDWXAWWOWVFUOZUPZAWVIURZCW
        WBURZUJUTWXGWWCWXPUAUJWVDWWOVQZWVHWXNCAWWBWVIWXQWVEWXAWVGWXMWVDWWOIWKWV
        DWWOWVFWPWMUVAUVDWXPWXFUJWXOWXECUIWWBWVFWWNVQZWXNWXCAWVIWXDWVFWWNPVKWXR
        WXMWXBWXAWVFWWNWWOWHWEUVEVOXJXKUVFXLXKVVQOWXIUVGXMWTWVJCUGVWOWXKCUGVWNV
        VRCUGVVQOCUGOWWFUDWWESTCUGCUGUGWWAWWDVXEWWAUKZCVIWWCCUAUGWVJCWWBXNXOXPX
        QXRVXEVVQUKZCVIXSVXEVWNUKZCVIXTZVXEWXKUKCVIYBXMXJWVHCAUAWXKWVIVWNWXJXHW
        VFPXHWVHUAUTWVGUAUTZWVFUVHWYCXIUAWVFUVIWVFCWRUVJUVMWVHWVGUAWVEWVGUVKUVL
        UVNUVOUVPZWWOVWOVQZWWQWVLWWNWYEWWPWVKUAUAUGUGWWOVWOVXEWWOUKZUAVIUAUGVWN
        VVRUAUGVVQOUAUGOWWFUDWWESTUAUGUAUGUGWWAWWDWXSUAVIWWCUAUGUVQXPXQXRWXTUAV
        IXSWYAUAVIXTXPWVJCUGWWOVWOWYFCVIWYBYBUVRWGWWNWVLWVLWIWJVRYCVWOWVLWWFWYD
        UVSUWCXAXBXCXDUVTYDVXDWURWVCYAVUIWUDVXDWUQWVBEIVXDWUPWVAVUDVXDWUOWUTVXD
        WUNWUSPVXDVWNUWAUKWUNWUSVQVWNUWDVVQVWNOUWBYLVLVMYEWDWOUWEWUIWURUEWULVCV
        VPWULVQZWUFWUQEIWYGVWAWUPVUDWYGVVTWUOWYGVVSWUNPVVPWULVVRVKVLVMYEWDYFUWF
        UWGWUFUEEVCIUWHUWIWUGVWIEIUEVCVWAVUDUWJUWKUWLUWMUWNVYFVXSVYQVXHVYFVXFVY
        PVXOVXEVYEVUCYGUWOUWPUWQYHYIWSYJYHYIYJWSYHWQWSUWRYKVVOVWDVWEVVOVXAVWDVV
        OULVCUKVUCPULVVRVPZVFZVGZUKZVXAUWSVVOVUCPVVQVFZVGZWYJVVOVUCVVBPVPZWYMVV
        OVUCVYAVUFVVBUOZDQURZUPZBUTZWYNVVOVURWYODIURZVUCWYRUKVVJVURVUIVVMVUQVUR
        VVAUWTUXAVVMWYSVUIVVJVVLWYSVVCVVLVVBVVBUOZWYSVVBUXDWYOWYTDVVBIVUFVVBVVB
        WPYFUXEUXBWOWYQVURWYSUPBVUCVYTWUAVYAVURWYPWYSWUBWUAWYODQIWUCWLWMWNUXCVV
        OWYRXIUKZPXEZUPVVBWYRVSZPUKZWYNWYRVQVVOXUAXUBVUBXUAVUHVVNVUBWYRWYPBRYMX
        IWYPBRYNWYPBRHYOYPZYKXUBVYNXEJLVYLXFPVYNUCUXFYCZUXGVUBXUDVUHVVNVUBXUDWY
        RWYRVQZWYRXGVUBVVBXIUKZXUAXUDXUGYAFWRZXUEXUDXUCVYNUKXUHXUAUPXUGPVYNXUCU
        CWCVYMVYGWYRVQXUGJLVVBWYRXIXIVYHVVBVQZVYLWYRVYGXUJVYKWYQBXUJVYJWYPVYAXU
        JVYIWYODQVYHVVBVUFWHWDWEWFWGVYGWYRWYRWIUXHUXIYQUXJYKVVBWYRXIPUXKUXNUXLV
        VOWYNWYNVDZVGZWYMWYNVVBPXHUXMVUBXULWYMVQZVUHVVNVUBPXIUYCZXUHXUMVUBVYLXI
        UKZJXIUSXUNVUBXUOJXIVUBVYLVYJBRYMXIVYJBRYNVYJBRHYOYPUXOJLXIVYLPPVYNVYHX
        IUKZVYMUPZJLWBUCVYMXUQJLXUPVYMJWRUXPXLXKUXQYDXUIXUNXUHUPXUKWYLXIVVBPUYD
        VMUXRYKUXSUXTWYIWYLWYHVVQPVVQOVVBUYAUYEXAXBUYBVWTWYKUEULVCVVPULVQZVWAWY
        JVUCXURVVTWYIXURVVSWYHPVVPULVVRVKVLVMVNYFYQVXBYRUYFUYGUYHVWCVWLVWFVUOVW
        LNVWBUEVCVWAUYIVVTXUBVVTXIUKXUFPVVSVVPVVRXHUYOXMUYJUYKVUJVWBVQZVUKVWHVU
        NVWKVUJVWBRWPXUSVUNVUMAVWBUSVWKVUMAUGVUJVWBVXEVUJUKAVIZVXEVWBUKVXEVWAUK
        ZUEVCURAUEVXEVCVWAVJXVAAUEVCVVPVCUKAVIAUGVVTAUGPVVSVXEPUKAVIAUGVVPVVRAU
        GVVQOAUGOWWFUDWWESTAUGAUGUGWWAWWDWXSAVIWWCAUAUGWVJACWWBWVFWWBUKAVIWVHAW
        VIXNYSXOXPXQXRWXTAVIXSVXEVVPUKAVIXTUYLUYMYSUYNZUYPXUSVUMVWJAVWBAUGUGVUJ
        VWBXUTXVBXPXUSVULVWIEIVUJVWBVUDWHWDUYQUYRWMWNUYSYRVVHVWFKVWBVUPVVEVWBVQ
        VVFVWDVVGVWEVVEVWBVUCYGVVEVWBVUTWPWMYFYLYTUYTWQYTYIVUA $.
        $( [12-Sep-2009] $)
    $}

    $( In the topology generated by a neighborhood base, a set is a
       neighborhood of a point iff it contains a subset in the base.
       (Contributed by Jeff Hankins, 9-Sep-2009.) $)
    neibastop2 $p |- ( ( X e. B /\ A. x e. X ( U =/= (/) /\ A. v e. U ( ( x e.
    v /\ v C_ X ) /\ ( A. w e. U E. t e. U t C_ ( v i^i w ) /\ E. t e. U A. y
    e. t E. w e. W w C_ v ) ) ) ) -> ( x e. X -> ( n e. ( ( nei ` { o | ( o C_
    X /\ A. x e. o E. v e. U v C_ o ) } ) ` { x } ) <-> ( n C_ X /\ E. u e. U u
    C_ n ) ) ) ) $=
      ( va wcel cv wss wa wrex wral vm vg vz vf vh vb wne cin cab ctop csn cnei
      vc c0 cfv wb wi neibastop1 cuni eqid neii1 ex wal unissb df-ral vex sseq1
      wceq sseq2 rexbidv raleqbi1dv anbi12d elab imbi1i albii simpl mpgbi sstr2
      3bitrri syl6mpi neii2 snss ra4 rcla4ev syl9r rexlimiv syl6 syl5bir adantl
      imp3a sylbi jcad ad2antrl simprll wex hbra1 simplr imim2i ancld syl eximd
      df-rex 3imtr4g impcom ralimi ad2antlr ssid jctil elssabg ad2antrr elssuni
      mpbird simprlr sseldd isneip syl2anc sstrd copab neibastop2lem4 mpbir2and
      n0 simprrl expr impbid exp32 mpd ) LGOZHUNUGZAPZDPZOZYJLQZRFPZYJCPZUHQFHS
      CHTYNYJQCKSBYMTFHSRZRZDHTZRZALTZRZJPZLQZYJUUAQZDHSZAUUATZRZJUIZUJOZYILOZI
      PZYIUKZUUGULUOUOOZUUJLQZEPZUUJQZEHSZRZUPZUQABCDFGHJKLMURYTUUHUUIUURYTUUHU
      UIRZRUULUUQUUHUULUUQUQYTUUIUUHUULUUMUUPUUHUULUUJUUGUSZQZUUTLQZUUMUUHUULUV
      AUUKUUGUUJUUTUUTUTZVAVBNPZLQZYJUVDQZDHSZAUVDTZRZUVEUQZUVBNUVBUVENUUGTUVDU
      UGOZUVEUQZNVCUVJNVCNUUGLVDUVENUUGVEUVLUVJNUVKUVIUVEUUFUVIJUVDNVFUUAUVDVHZ
      UUBUVEUUEUVHUUAUVDLVGUUDUVGAUUAUVDUVMUUCUVFDHUUAUVDYJVIVJVKVLVMVNVOVSUVEU
      VHVPVQUUJUUTLVRVTUUHUULUUKUAPZQZUVNUUJQZRZUAUUGSZUUPUUHUULUVRUUKUAUUGUUJW
      AVBUVQUUPUAUUGUVNUUGOUVNLQZYJUVNQZDHSZAUVNTZRZUVQUUPUQZUUFUWCJUVNUAVFUUAU
      VNVHZUUBUVSUUEUWBUUAUVNLVGUUDUWAAUUAUVNUWEUUCUVTDHUUAUVNYJVIVJVKVLVMUWBUW
      DUVSUWBUVOUVPUUPUVOYIUVNOZUWBUVPUUPUQZYIUVNAVFWBUWBUWFUWAUWGUWAAUVNWCUVTU
      WGDHUVTUVPYJUUJQZYJHOZUUPYJUVNUUJVRUWIUWHUUPUUOUWHEYJHUUNYJUUJVGWDVBWEWFW
      GWHWJWIWKWFWGWLWMYTUUSUUQUULYTUUSUUQRZRZUULUVAYIUBPZOUWLUUJQRUBUUGSZUWKUU
      HYIUUTOUULUVAUWMRUPYTUUHUUIUUQWNUWKLUUTYIUWKLUUGOZLUUTQUWKUWNLLQZYLDHSZAL
      TZRZUWKUWQUWOYSUWQYGUWJYRUWPALYQYHUWPYQUWIDWOUWIYLRZDWOYHUWPYQUWIUWSDYPDH
      WPYQUWIYPUQZUWIUWSUQYPDHWCUWTUWIYLYPYLUWIYKYLYOWQWRWSWTXADHYAYLDHXBXCXDXE
      XFLXGXHYGUWNUWRUPYSUWJUUEUWQJLLGUUDUWPAUUALUUALVHUUCYLDHUUALYJVIVJVKXIXJX
      LLUUGXKWTZYTUUHUUIUUQXMXNYIUBUUGUUJUUTUVCXOXPUWKUUJLUUTYTUUSUUMUUPYBUXAXQ
      ABUCCDEFGHUDUBUEIJUFPUMPZHOUXBUCPZQRAUXCUEPBPLOYNUDPQCKSRBUIVHUDUEXRZUOSU
      CUVDSUMUIVHNUFXRZUXDKLNUFUMMUXDUTUXEUTXSXTYCYDYEYF $.
      $( [9-Sep-2009] $)

    $( The topology generated by a neighborhood base is unique.  (Contributed
       by Jeff Hankins, 16-Sep-2009.) $)
    neibastop3 $p |- ( ( X e. B /\ A. x e. X ( U =/= (/)
          /\ A. v e. U ( ( x e. v /\ v C_ X ) /\ ( A. w e. U E. t e. U t
            C_ ( v i^i w ) /\ E. t e. U A. y e. t E. w e. W w C_ v ) ) ) )
     -> E! j e. Top ( X = U. j /\ A. x e. X A. n ( n e. ( ( nei ` j ) ` { x } )
        <-> ( n C_ X /\ E. u e. U u C_ n ) ) ) ) $=
      ( va vm wcel cv wss wa wral vo vk c0 wne cin wrex ctop cuni wceq csn cnei
      cfv wb wal weu wreu cab wi neibastop1 wex n0 ax-17 hbra1 hbre1 hbim com12
      ra4 adantl ra4e adantld adantrd syld exlimd syl5bi imp3a ralimdv imp ssid
      ex jctil sseq2 rexbidv raleqbi1dv elssabg adantr mpbird syl unissb df-ral
      elssuni vex sseq1 anbi12d elab imbi1i albii 3bitrri simpl mpgbi a1i eqssd
      hban neibastop2 alrimdv ralrimi jca32 eqid ad2ant2rl simprl sseldd simplr
      neii1 eleqtrrd exp32 adantrr imp31 adantlll sylan adantlr sseldi adantllr
      jca bibi12d a4v syl6 adantll bitr4d pm5.21nd ralbida opnnei ad2antrl hbab
      eleq1 hbfv sneq fveq2d eleq2d cbvral syl6bb 3bitr4d alrimiv eqeq2d fveq1d
      eqrdv unieq hbeq fveq2 bibi1d albidv ralbid eqeu syl3anc df-reu sylibr )
      LGPZHUCUDZAQZDQZPZUURLRZSZFQZUURCQZUERFHUFCHTUVCUURRCKUFBUVBTFHUFSZSZDHTZ
      SZALTZSZIQZUGPZLUVJUHZUIZJQZUUQUJZUVJUKULZULZPZUVNLRZEQZUVNRZEHUFZSZUMZJU
      NZALTZSZSZIUOZUWGIUGUPUVIUAQZLRZUURUWJRZDHUFZAUWJTZSZUAUQZUGPZUWQLUWPUHZU
      IZUVNUVOUWPUKULZULZPZUWCUMZJUNZALTZSZSZUWHUVJUWPUIZURZIUNUWIABCDFGHUAKLMU
      SZUVIUWQUWSUXEUXJUVILUWRUVILUWPPZLUWRRUVIUXKLLRZUUTDHUFZALTZSZUVIUXNUXLUU
      OUVHUXNUUOUVGUXMALUUOUUPUVFUXMUUPUURHPZDUTUUOUVFUXMURZDHVAUUOUXPUXQDUUODV
      BUVFUXMDUVEDHVCUUTDHVDVEUUOUXPUXQUUOUXPSZUVFUVEUXMUXPUVFUVEURUUOUVFUXPUVE
      UVEDHVGVFVHUXRUVAUXMUVDUXRUUTUXMUUSUXPUUTUXMURUUOUXPUUTUXMUUTDHVIVSVHVJVK
      VLVSVMVNVOVPVQLVRVTUUOUXKUXOUMUVHUWNUXNUALLGUWMUXMAUWJLUWJLUIUWLUUTDHUWJL
      UURWAWBWCWDWEWFLUWPWJWGUWRLRZUVINQZLRZUURUXTRZDHUFZAUXTTZSZUYAURZUXSNUXSU
      YANUWPTUXTUWPPZUYAURZNUNUYFNUNNUWPLWHUYANUWPWIUYHUYFNUYGUYEUYAUWOUYEUAUXT
      NWKUWJUXTUIZUWKUYAUWNUYDUWJUXTLWLUWMUYCAUWJUXTUYIUWLUYBDHUWJUXTUURWAWBWCW
      MWNWOWPWQUYAUYDWRWSZWTXAUVIUXDALUUOUVHAUUOAVBUVGALVCXBZUVIUUQLPZUXCJABCDE
      FGHJUAKLMXCXDXEXFUVIUXIIUVIUWHUXHUVIUWHSZOUVJUWPUYMOQZUVQPZAUYNTZUYNUXAPZ
      AUYNTZUYNUVJPZUYNUWPPZUYMUYOUYQAUYNUVIUWHAUYKUVKUWGAUVKAVBUVMUWFAUVMAVBUW
      EALVCXBXBXBUYMUUQUYNPZSZUYOUYQVUBUYLSVUBUYOSVUBUYLVUBUYOWRUWHVUAUYOUYLUVI
      UWHVUAUYOUYLUVKUVMVUAUYOUYLURURUWFUVKUVMSZVUAUYOUYLVUCVUAUYOSZSZUUQUVLLVU
      EUYNUVLUUQUVKUYOUYNUVLRUVMVUAUVOUVJUYNUVLUVLXGXLXHVUCVUAUYOXIXJUVKUVMVUDX
      KXMXNXOXPXQYBVUBUYQSVUBUYLVUBUYQWRUVIVUAUYQUYLUWHUVIVUASUYQSZUWRLUUQUYJVU
      FUYNUWRUUQUVIUYQUYNUWRRZVUAUVIUWQUYQVUGUXJUVOUWPUYNUWRUWRXGXLXRXSUVIVUAUY
      QXKXJXTYAYBUYMUYLUYOUYQUMVUAUYMUYLSUYOUYNLRZUVTUYNRZEHUFZSZUYQUWHUYLUYOVU
      KUMZUVIUWGUYLVULUVKUWFUYLVULUVMUWFUYLVULUWFUYLUWEVULUWEALVGUWDVULJOUVNUYN
      UIZUVRUYOUWCVUKUVNUYNUVQYMVUMUVSVUHUWBVUJUVNUYNLWLVUMUWAVUIEHUVNUYNUVTWAW
      BWMYCYDYEVQYFYFYFUVIUYLUYQVUKUMZUWHUVIUYLVUNABCDEFGHOUAKLMXCVQXSYGXSYHYIU
      VKUYSUYPUMUVIUWGAUYNUVJYJYKUVIUYTUYRUMZUWHUVIUWQVUOUXJUWQUYTUYNUBQZUJZUWT
      ULZPZUBUYNTUYRUBUYNUWPYJVUSUYQUBAUYNAOVUQUWTAOUWPUKUYNUKPAVBUWOAUAOUWKUWN
      AUWKAVBUWMAUWJVCXBYLZYNUYNVUQPAVBYNUYQUBVBVUPUUQUIZVURUXAUYNVVAVUQUVOUWTV
      UPUUQYOYPYQYRYSWGWEYTUUDVSUUAUWHUXGIUWPUGUXHUVKUWQUWGUXFUVJUWPUGYMUXHUVMU
      WSUWFUXEUXHUVLUWRLUVJUWPUUEUUBUXHUWEUXDALAOOUVJUWPUYSAVBVUTUUFUXHUWDUXCJU
      XHUVRUXBUWCUXHUVQUXAUVNUXHUVOUVPUWTUVJUWPUKUUGUUCYQUUHUUIUUJWMWMUUKUULUWG
      IUGUUMUUN $.
      $( [16-Sep-2009] $)
  $}

