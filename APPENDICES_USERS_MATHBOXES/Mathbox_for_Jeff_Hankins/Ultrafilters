$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Ultrafilters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c UFil $.
  $( Extend class notation with the class of all ultrafilters. $)
  cufil $a class UFil $.

  ${
    $d f x $.
    $( Define the class of all ultrafilters. $)
    df-ufil $a |- UFil = { f e. Fil | A. x e. ~P U. f ( x e. f \/ ( U. f \ x )
    e. f ) } $.
  $}

  ${
    $d f t w x y z F $.  $d f t w x y z X $.
    isufil.1 $e |- X = U. F $.
    $( The property of being an ultrafilter.  (Contributed by Jeff Hankins,
       30-Nov-2009.) $)
    isufil $p |- ( F e. UFil <-> ( F e. Fil /\ A. x e. ~P X ( x e. F \/ ( X \ x
    ) e. F ) ) ) $=
      ( vf cv wcel cuni cdif cpw wral cfil cufil wceq unieq syl6eqr pweqd eleq2
      wo difeq1d id eleq12d orbi12d raleqbidv df-ufil elrab2 ) AFZEFZGZUHHZUGIZ
      UHGZSZAUJJZKUGBGZCUGIZBGZSZACJZKEBLMUHBNZUMURAUNUSUTUJCUTUJBHCUHBODPZQUTU
      IUOULUQUHBUGRUTUKUPUHBUTUJCUGVATUTUAUBUCUDAEUEUF $.
      $( [30-Nov-2009] $)
  $}

  ${
    $d x F $.
    $( An ultrafilter is a filter.  (Contributed by Jeff Hankins,
       1-Dec-2009.) $)
    ufilfil $p |- ( F e. UFil -> F e. Fil ) $=
      ( vx cufil wcel cfil cv cuni cdif wo cpw wral eqid isufil simplbi ) ACDAE
      DBFZADAGZOHADIBPJKBAPPLMN $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d f x y F $.  $d x S $.  $d f x y X $.
    ufilss.1 $e |- X = U. F $.
    $( For any subset of the base set of an ultrafilter, either the set is in
       the ultrafilter or the complement is.  (Contributed by Jeff Hankins,
       1-Dec-2009.) $)
    ufilss $p |- ( ( F e. UFil /\ S C_ X ) -> ( S e. F \/ ( X \ S ) e. F ) ) $=
      ( vx cufil wcel wss cdif wo cpw cvv wb cuni uniexg syl5eqel elpw2g syl cv
      wral cfil isufil simprbi wceq eleq1 difeq2 eleq1d orbi12d rcla4cv sylbird
      wi imp ) BFGZACHZABGZCAIZBGZJZUMUNACKZGZURUMCLGUTUNMUMCBNLDBFOPACLQRUMESZ
      BGZCVAIZBGZJZEUSTZUTURUKUMBUAGVFEBCDUBUCVEUREAUSVAAUDZVBUOVDUQVAABUEVGVCU
      PBVAACUFUGUHUIRUJUL $.
      $( [1-Dec-2009] $)

    $( The complement is in an ultrafilter iff the set is not.  (Contributed by
       Mario Carneiro, 8-Dec-2013.) $)
    ufilb $p |- ( ( F e. UFil /\ S C_ X ) ->
                  ( -. S e. F <-> ( X \ S ) e. F ) ) $=
      ( cufil wcel wss wa wn cdif ufilss wi cfil cfbas ufilfil filfbas fbncp ex
      ord con2d 3syl adantr impbid ) BEFZACGZHZABFZIZCAJBFZUFUGUIABCDKSUDUIUHLZ
      UEUDBMFBNFZUJBOBPUKUGUIUKUGUIIACBQRTUAUBUC $.
      $( [11-Dec-2013] $)

    ${
      $d x G $.  $d x Y $.
      ufilmax.2 $e |- Y = U. G $.
      $( Any filter finer than an ultrafilter is actually equal to it.
         (Contributed by Jeff Hankins, 1-Dec-2009.) $)
      ufilmax $p |- ( ( ( F e. UFil /\ G e. Fil ) /\ ( X = Y /\ F C_ G ) ) ->
                      F = G ) $=
        ( vx cufil wcel cfil wa wceq wss simprr cv cuni elssuni wb wi wn ex syl
        syl6sseqr sseq2 ad2antrl syl5ibr cdif ufilb simprlr sseld cfbas filfbas
        ad2ant2rl fbncp con2d ad2antlr syld sylbid con4d expr com23 ssrdv eqssd
        mpdd ) AHIZBJIZKZCDLZABMZKZKZABVGVHVINVKGBAVKGOZBIZVLCMZVLAIZVMVNVKVLDM
        ZVMVLBPDVLBQFUCVHVNVPRVGVICDVLUDUEUFVKVNVMVOVGVJVNVMVOSVGVJVNKZKZVOVMVR
        VOTZCVLUGZAIZVMTZVEVNVSWARVFVJVLACEUHUMVRWAVTBIZWBVRABVTVGVHVIVNUIUJVFW
        CWBSVEVQVFVMWCVFBUKIZVMWCTZSBULWDVMWEVLCBUNUAUBUOUPUQURUSUTVAVDVBVC $.
        $( [8-Dec-2013] $) $( [1-Dec-2009] $)
    $}

    $( The maximal property of an ultrafilter.  (Contributed by Jeff Hankins,
       30-Nov-2009.)  (The proof was shortened by Mario Carneiro,
       8-Dec-2013.) $)
    isufil2 $p |- ( F e. UFil <-> ( F e. Fil /\ A. f e. Fil ( ( X = U. f /\ F
    C_ f ) -> F = f ) ) ) $=
      ( vx vy wcel cfil cv cuni wceq wss wa wi wral wn c0 wne cvv syl ralrimiva
      cufil ufilfil eqid ufilmax ex jca cdif wo cpw simpl elpwi cin csn cun cfi
      cfv cfg uniun vex unisn eqcomi uneq12i eqtr4i simplr ssequn2 sylib simpll
      syl5req snex unexg fiuni cfbas simpr wsbc ralsn ineq2 neeq1d sbcie bitr2i
      sylancl ralbii wb filfbas ad2antrr inss2 filusb adantr ineq1 rcla4va ssn0
      sylan sylancr snfil fbunfip syl2anc mpbird ssun2 snnz mp2an jctil fsubbas
      fgbas 3eqtrd ssun1 ssfii fbssfg sstrd syl5ss fgfil eqeq2d anbi12d imbi12d
      unieq sseq2 eqeq2 rcla4v 3syl mpid snid sselii a1i sseldd syl5ibrcom syld
      eleq2 impancom an32s con3d wrex rexnal nne elssuni syl6sseqr adantl difss
      reldisj fillsb 3expd mpii imp sylbid syl5bi rexlimdva syl5bir orrd sylan2
      isufil sylanbrc impbii ) BUBGZBHGZCAIZJZKZBUUMLZMZBUUMKZNZAHOZMZUUKUULUUT
      BUCUUKUUSAHUUKUUMHGMUUQUURBUUMCUUNDUUNUDUEUFUAUGUVAUULEIZBGZCUVBUHZBGZUIZ
      ECUJZOUUKUULUUTUKUVAUVFEUVGUVBUVGGUVAUVBCLZUVFUVBCULUVAUVHMZUVCUVEUVIUVCP
      FIZUVBUMZQRZFBOZPZUVEUVIUVMUVCUULUVHUUTUVMUVCNUULUVHMZUVMUUTUVCUVOUVMMZUU
      TBBUVBUNZUOZUPUQZURUQZKZUVCUVPUUTCUVTJZKZBUVTLZMZUWAUVPUWCUWDUVPCUVRJZUVS
      JZUWBUVPUWFCUVBUOZCUWFBJZUVQJZUOUWHBUVQUSCUWIUVBUWJDUWJUVBUVBEUTZVAVBVCVD
      UVPUVHUWHCKUULUVHUVMVEUVBCVFVGVIUVPUVRSGZUWFUWGKUVPUULUVQSGUWLUULUVHUVMVH
      UVBVJBUVQHSVKWAZUVRSVLTUVPUVSVMGZUWGUWBKUVPUWNUVRQRZQUVSGPZMZUVPUWPUWOUVP
      UWPUVJUUMUMZQRZAUVQOZFBOZUVPUVMUXAUVOUVMVNUVLUWTFBUWTUWSAUVBVOUVLUWSAUVBU
      WKVPUWSUVLAUVBUWKUUMUVBKUWRUVKQUUMUVBUVJVQVRVSVTWBVGUVPBVMGZUVQVMGZUWPUXA
      WCUULUXBUVHUVMBWDWEUVPUVBSGZUVBQRZUXCUWKUVPCUVBUMZUVBLUXFQRZUXECUVBWFUVOC
      BGZUVMUXGUULUXHUVHBCDWGWHUVLUXGFCBUVJCKUVKUXFQUVJCUVBWIVRWJWLUXFUVBWKWMUX
      DUXEMUVQHGUXCUVBSWNUVQWDTWMFABUVQWOWPWQUVQUVRLUVQQRUWOUVQBWRZUVBUWKWSUVQU
      VRWKWTXAUVPUWLUWNUWQWCUWMUVRSXBTWQZUVSUWGUWGUDXCTXDUVPBUVRUVTBUVQXEUVPUVR
      UVSUVTUVPUWLUVRUVSLUWMUVRSXFTUVPUWNUVSUVTLUXJUVSXGTXHZXIUGUVPUWNUVTHGUUTU
      WEUWANZNUXJUVSXJUUSUXLAUVTHUUMUVTKZUUQUWEUURUWAUXMUUOUWCUUPUWDUXMUUNUWBCU
      UMUVTXNXKUUMUVTBXOXLUUMUVTBXPXMXQXRXSUVPUVCUWAUVBUVTGUVPUVRUVTUVBUXKUVBUV
      RGUVPUVQUVRUVBUXIUVBUWKXTYAYBYCBUVTUVBYFYDYEYGYHYIUULUVNUVENUUTUVHUVNUVLP
      ZFBYJUULUVEUVLFBYKUULUXNUVEFBUXNUVKQKZUULUVJBGZMZUVEUVKQYLUXQUXOUVJUVDLZU
      VEUXPUXOUXRWCZUULUXPUVJCLUXSUXPUVJUWICUVJBYMDYNUVJUVBCYQTYOUULUXPUXRUVENZ
      UULUXPUVDCLZUXTCUVBYPUULUXPUYAUXRUVEUVJUVDBCDYRYSYTUUAUUBUUCUUDUUEWEYEUUF
      UUGUAEBCDUUHUUIUUJ $.
      $( [8-Dec-2013] $) $( [30-Nov-2009] $)

    $( An ultrafilter is a prime filter.  (Contributed by Jeff Hankins,
       1-Jan-2010.) $)
    ufprim $p |- ( ( F e. UFil /\ A C_ X /\ B C_ X ) -> ( ( A e. F \/ B e. F )
    <-> ( A u. B ) e. F ) ) $=
      ( wcel wss w3a cun wa 3ad2ant1 adantr simpr a1i 3jca fillsb sylc cdif cin
      ex cufil wo cfil ufilfil unss biimpi 3adant1 ssun1 ssun2 jaod wn wb ufilb
      3adant3 difun2 uncom difeq1i eqtr3i ineq2i indifcom 3eqtr4i filint simp13
      syl3an1 syl5eqel inss1 3expia sylbid orrd impbid ) CUAFZADGZBDGZHZACFZBCF
      ZUBZABIZCFZVNVOVSVPVNVOVSVNVOJZCUCFZVOVRDGZAVRGZHVSVNWAVOVKVLWAVMCUDKZLVT
      VOWBWCVNVOMVNWBVOVLVMWBVKVLVMJWBABDUEUFUGZLWCVTABUHNOAVRCDEPQTVNVPVSVNVPJ
      ZWAVPWBBVRGZHVSVNWAVPWDLWFVPWBWGVNVPMVNWBVPWELWGWFBAUINOBVRCDEPQTUJVNVSVQ
      VNVSJZVOVPWHVOUKZDARZCFZVPVNWIWKULZVSVKVLWLVMACDEUMUNLVNVSWKVPVNVSWKHZWAB
      WJSZCFZVMWNBGZHVPVNVSWAWKWDKWMWOVMWPWMWNVRWJSZCDBARZSDVRARZSWNWQWRWSDBAIZ
      ARWRWSBAUOWTVRABAUPUQURUSBDAUTVRDAUTVAVNWAVSWKWQCFWDVRWJCVBVDVEVKVLVMVSWK
      VCWPWMBWJVFNOWNBCDEPQVGVHVITVJ $.
      $( [8-Dec-2013] $) $( [1-Jan-2010] $)
  $}

  ${
    $d f g h x y F $.  $d f g h x y X $.
    filssufil.1 $e |- X = U. F $.
    $( A filter is contained in some ultrafilter.  (Requires the Axiom of
       Choice, via ~ zornn0 .)  (Contributed by Jeff Hankins, 2-Dec-2009.)
       (The proof was shortened by Mario Carneiro, 11-Dec-2013.) $)
    filssufil $p |- ( F e. Fil -> E. f e. UFil ( X = U. f /\ F C_ f ) ) $=
      ( vh vg vx cfil wcel cv cuni wceq wss wa wi wral wrex cvv sseq2 syl vy wn
      wpss cufil crab c0 wne wo w3a wal cpw rabss eqimss2 adantr vex elpw bitri
      sspwuni sylibr a1i mprgbir uniexg syl5eqel pwexg 3syl sylancr ssid pm3.2i
      ssexg jctr unieq eqeq2d anbi12d elrab ne0i cun simpr1 ssrab simpld simpr2
      sylib eqcomd ralimi adantl simpr3 ssequn1 eleq1a ssequn2 ralimdva ralimia
      simpl syl5bi jaod uneq1 eleq1d ralbidv cbvralv r19.26 filuni syl3anc ciun
      wex eluni rexbii eliun eluni2 anbi2i r19.42v bitr4i exbii rexcom4 3bitr4i
      sylanbrc eqriv iuneq2 syl5eqr iunconst eqtr2d ssel2 simprr ssuni sylancom
      n0 ex exlimdv jca32 alrimiv jca neeq1 3anbi1d eleq2 syl6bb imbi12d albidv
      sylc raleq ralrab rexeqbi1dv rexrab zornn0 vtoclg anbi1d biimprd simplbi2
      eqeq1 sstr2 anim2d sylan9 imim1d df-pss necon1bd syl6 ralimdv anim2i eqid
      a2i imp isufil2 simprl reximi2 ) BHIZCAJZKZLZBUVBMZNZCEJZKZLZBUVGMZNZUVBU
      VGUCZUBZOZEHPZNZAHQZUVFAUDQUVACFJZKZLZBUVRMZNZFHUEZRIZUWCUFUGZGJZUWCMZUWF
      UFUGZUVBUVGMZUVGUVBMZUHZEUWFPZAUWFPZUIZUWFKZHIZCUWOKZLZBUWOMZNZNZOZGUJZNZ
      UVQUVAUWCCUKZUKZMZUXFRIZUWDUXGUWBUVRUXFIZOZFHUWBFHUXFULUXJUVRHIUWBUVSCMZU
      XIUVTUXKUWAUVSCUMUNUXIUVRUXEMUXKUVRUXEFUOUPUVRCURUQUSUTVAUVACRIUXERIUXHUV
      ACBKZRDBHVBVCCRVDUXERVDVEUWCUXFRVIVFUVAUWEUXCUVABUWCIZUWEUVAUVACUXLLZBBMZ
      NZNUXMUVAUXPUXNUXODBVGVHVJUWBUXPFBHUVRBLZUVTUXNUWAUXOUXQUVSUXLCUVRBVKVLUV
      RBBSVMVNUSUWCBVOTUVAUXBGUVAUWNUXAUVAUWNNZUWPUWRUWSUXRUWPUWRUXRUWFHMZUWHUV
      SCLZUVRUVGVPZUWFIZEUWFPZNFUWFPZUWPUWRNUXRUXSUWBFUWFPZUXRUWGUXSUYENZUVAUWG
      UWHUWMVQZUWBFHUWFVRWAZVSUVAUWGUWHUWMVTZUXRUXTFUWFPZUYCFUWFPZUYDUXRUYFUYJU
      YHUYEUYJUXSUWBUXTFUWFUWBCUVSUVTUWAWKWBWCWDTZUXRUWMUYKUVAUWGUWHUWMWEUWMUVB
      UVGVPZUWFIZEUWFPZAUWFPUYKUWLUYOAUWFUVBUWFIZUWKUYNEUWFUYPUVGUWFIZNUWIUYNUW
      JUYQUWIUYNOUYPUWIUYMUVGLUYQUYNUVBUVGWFUVGUWFUYMWGWLWDUYPUWJUYNOUYQUWJUYMU
      VBLUYPUYNUVGUVBWHUVBUWFUYMWGWLUNWMWIWJUYOUYCAFUWFUVBUVRLZUYNUYBEUWFUYRUYM
      UYAUWFUVBUVRUVGWNWOWPWQWATUXTUYCFUWFWRXMFEUWFCWSWTVSUXRUWQFUWFCXAZCUXRUWQ
      FUWFUVSXAZUYSEUYTUWQUVGUVSIZFUWFQUVGUVBIZUVBUVRIZNZAXBZFUWFQZUVGUYTIUVGUW
      QIZVUAVUEFUWFAUVGUVRXCXDFUVGUWFUVSXEVUBUVBUWOIZNZAXBVUDFUWFQZAXBVUGVUFVUI
      VUJAVUIVUBVUCFUWFQZNVUJVUHVUKVUBFUVBUWFXFXGVUBVUCFUWFXHXIXJAUVGUWOXCVUDFA
      UWFXKXLXLXNUXRUYJUYTUYSLUYLFUWFUVSCXOTXPUXRUWHUYSCLUYIFUWFCXQTXRUXRUWGUWH
      UWSUYGUYIUWHUYQEXBUWGUWSEUWFYCUWGUYQUWSEUWGUYQUWSUWGUYQUVJUWSUWGUYQNZUVGH
      IZUVKNZUVJVULUVGUWCIVUNUWFUWCUVGXSUWBUVKFUVGHUVRUVGLZUVTUVIUWAUVJVUOUVSUV
      HCUVRUVGVKVLUVRUVGBSVMZVNWAVUMUVIUVJXTTBUVGUWFYAYBYDYEWLYOYFYDYGYHUAJZUFU
      GZUWFVUQMZUWHUWMUIZUWOVUQIZOZGUJZNZUVMEVUQPZAVUQQZOUXDUVQOUAUWCRVUQUWCLZV
      VDUXDVVFUVQVVGVURUWEVVCUXCVUQUWCUFYIVVGVVBUXBGVVGVUTUWNVVAUXAVVGVUSUWGUWH
      UWMVUQUWCUWFSYJVVGVVAUWOUWCIUXAVUQUWCUWOYKUWBUWTFUWOHUVRUWOLZUVTUWRUWAUWS
      VVHUVSUWQCUVRUWOVKVLUVRUWOBSVMVNYLYMYNVMVVGVVFUVOAUWCQUVQVVEUVOAVUQUWCVVG
      VVEUVMEUWCPUVOUVMEVUQUWCYPUWBUVKUVMEFHVUPYQYLYRUWBUVFUVOFAHUVRUVBLZUVTUVD
      UWAUVEVVIUVSUVCCUVRUVBVKVLUVRUVBBSVMYSYLYMAEGVUQUAUOYTUUAYOUVPUVFAHUDUVBH
      IZUVPNZUVBUDIZUVFVVKVVJUVCUVHLZUWINZUVBUVGLZOZEHPZNVVLUVPVVQVVJUVFUVOVVQU
      VFUVNVVPEHUVFUVNVVNUVMOVVPUVFVVNUVKUVMUVDVVNUVIUWINZUVEUVKUVDVVRVVNUVDUVI
      VVMUWICUVCUVHUUEUUBUUCUVEUWIUVJUVIBUVBUVGUUFUUGUUHUUIVVNUVMVVOUWIUVMVVOOV
      VMUWIUVLUVBUVGUVLUWIUVBUVGUGUVBUVGUUJUUDUUKWDUUPUULUUMUUQUUNEUVBUVCUVCUUO
      UURUSVVJUVFUVOUUSYHUUTT $.
      $( [11-Dec-2013] $) $( [2-Dec-2009] $)
  $}

  ${
    $d f g x F $.  $d f g x X $.
    ufileu.1 $e |- X = U. F $.
    $( If the ultrafilter containing a given filter is unique, the filter is an
       ultrafilter.  (Contributed by Jeff Hankins, 3-Dec-2009.)  (The proof was
       shortened by Mario Carneiro, 12-Dec-2013.) $)
    ufileu $p |- ( F e. Fil ->
        ( F e. UFil <-> E! f e. UFil ( X = U. f /\ F C_ f ) ) ) $=
      ( vg cfil wcel cufil cuni wceq wss wa wb wral wi wn c0 wne cvv syl eqcomd
      vx cv wrex wreu ufilfil eqid ufilmax ex sylan2 unieq syl6reqr eqimss2 jca
      impbid1 ralrimiva eqeq2 bibi2d ralbidv rcla4ev mpdan ibibr pm5.74ri sseq2
      eqeq2d anbi12d bitr3d rcla4va adantll simprd elssuni simpld sseq2d adantr
      syl5ibr cdif difeq1d csn cun cfi cfv cfg cfbas snex unexg sylancl fsubbas
      simpll emnfil ssun1 ssn0 mpan 3syl syl6sseqr df-ss ad2antll sseq1d fillsb
      cin sylib 3expd com23 imp3a sylbid con3d expr impr filusb ad2antrr difexg
      wsbc ralsng ineq2 neeq1d sbcieg bitrd inssdif0 necon3bbii syl6bbr filfbas
      mpbird ssdif0 eqss simplbi2 notbid biimpcd sylan9 adantl syl5bir necon2ad
      imp eleq1 mpd snfil syl2anc fbunfip mpbir2and fgfil filssufil sseldd syl5
      a1i unisng uneq12d syl6req uniun syl6eqr fiuni fgbas 3eqtrd eqeq1d anbi1d
      undifabs rexbidv r19.29 bi1 ssfii fbssfg sstrd syl5ss sstr2 anim2d imim1d
      syl56 rexlimdvw mpan2d an32s snidg elun2 adantlr eqeltrrd simpllr adantrr
      biimpa ufilb con4d mpdd ssrdv eqssd simplr eqeltrd rexlimdva impbid2 reu6
      a2i ) BFGZBHGZCAUCZIZJZBUWHKZLZUWHEUCZJZMZAHNZEHUDZUWLAHUEUWFUWGUWQUWGUWL
      UWHBJZMZAHNZUWQUWGUWSAHUWGUWHHGZLUWLUWRUXAUWGUWHFGZUWLUWROUWHUFUWGUXBLZUW
      LUWRUXCUWLLBUWHBUWHCUWIDUWIUGUHUAUIUJUWRUWJUWKUWRUWIBIZCUWHBUKDULBUWHUMUN
      UOUPUWPUWTEBHUWMBJZUWOUWSAHUXEUWNUWRUWLUWMBUWHUQURUSUTVAUWFUWPUWGEHUWFUWM
      HGZLZUWPUWGUXGUWPLZBUWMHUXHBUWMUXHCUWMIZJZBUWMKZUXFUWPUXJUXKLZUWFUWOUXLAU
      WMHUWNUWLUWOUXLUWNUWLUWOUWNUWLVBVCUWNUWJUXJUWKUXKUWNUWIUXICUWHUWMUKVEUWHU
      WMBVDVFVGVHVIZVJUXHUBUWMBUXHUBUCZUWMGZUXNCKZUXNBGZUXOUXPUXHUXNUXIKZUXNUWM
      VKUXHCUXIUXNUXHUXJUXKUXMVLZVMZVOUXHUXPUXOUXQUXHUXPUXOUXQOUXHUXPLUXQUXOUXH
      UXPUXQPZUXOPZUXHUXPUYALZLZUYBUXIUXNVPZUWMGZUYDCUXNVPZUYEUWMUYDCUXIUXNUXHU
      XJUYCUXSVNVQUYDBUYGVRZVSZVTWAZWBWAZUWMUYGUXGUYCUWPUYKUWMKZUXGUYCLZUWPUYLU
      YMUWPUWJUYKUWHKZLZAHUDZUYLUYMUYPUYKIZUWIJZUYNLZAHUDZUYMUYKFGZUYTUYMUYJWCG
      ZVUAUYMVUBUYIQRZQUYJGPZUYMUYISGZVUBVUCVUDLMUYMUWFUYHSGVUEUWFUXFUYCWHZUYGW
      DBUYHFSWEWFZUYISWGTUYMUWFBQRZVUCVUFBWIBUYIKVUHVUCBUYHWJZBUYIWKWLWMUYMVUDU
      WHUWMWSZQRZEUYHNZABNZUYMVULABUYMUWHBGZLVULUWHCWSZUXNKZPZUYMVUNVUQUXGUXPUY
      AVUNVUQOUXGUXPLVUNUYAVUQUXGUXPVUNUYAVUQOUXGUXPVUNLZLZVUPUXQVUSVUPUWHUXNKZ
      UXQVUSVUOUWHUXNVUNVUOUWHJZUXGUXPVUNUWHCKVVAVUNUWHUXDCUWHBVKDWNUWHCWOWTWPW
      QUXGVURVUTUXQOZUWFVURVVBOUXFUWFUXPVUNVVBUWFVUNUXPVVBUWFVUNUXPVUTUXQUWHUXN
      BCDWRXAXBXCVNYKXDXEXFXBXGYKUYMVULVUQMZVUNUYMUYGSGZVVCUYMCBGZVVDUWFVVEUXFU
      YCBCDXHXIZCUXNBXJTZVVDVULUWHUYGWSZQRZVUQVVDVULVUKEUYGXKVVIVUKEUYGSXLVUKVV
      IEUYGSUWMUYGJVUJVVHQUWMUYGUWHXMXNXOXPVUPVVHQUWHCUXNXQXRXSTVNYAUPUYMBWCGZU
      YHWCGZVUDVUMMUWFVVJUXFUYCBXTXIUYMUYHFGZVVKUYMVVDUYGQRZVVLVVGUYMVVEVVMVVFU
      YMVVEUYGQUYGQJCUXNKZUYMVVEPZCUXNYBUYCVVNVVOOUXGUXPVVNUXNCJZUYAVVOVVPUXPVV
      NUXNCYCYDVVPUYAVVOVVPUXQVVEUXNCBYLYEYFYGYHYIYJYMUYGSYNYOUYHXTTAEBUYHYPYOY
      AYQZUYJYRTAUYKUYQUYQUGYSTUYMUYOUYSAHUYMUWJUYRUYNUYMCUYQUWIUYMCUYIIZUYJIZU
      YQUYMCUXDUYHIZVSZVVRUYMVWACUYGVSCUYMUXDCVVTUYGUYMCUXDCUXDJUYMDUUBUAUYMVVD
      VVTUYGJVVGUYGSUUCTUUDCUXNUUMUUEBUYHUUFUUGUYMVUEVVRVVSJVUGUYISUUHTUYMVUBVV
      SUYQJVVQUYJVVSVVSUGUUITUUJUUKUULUUNYAUWPUYPLUWOUYOLZAHUDUYMUYLUWOUYOAHUUO
      UYMVWBUYLAHUYMUWOUYOUYLUWOUWLUWNOUYMUYOUWNOUYOUYLOUWLUWNUUPUYMUYOUWLUWNUY
      MUYNUWKUWJUYMBUYKKUYNUWKOUYMBUYIUYKVUIUYMUYIUYJUYKUYMVUEUYIUYJKVUGUYISUUQ
      TUYMVUBUYJUYKKVVQUYJUURTUUSZUUTBUYKUWHUVATUVBUVCUYOUWNUYLUYNUWNUYLOUWJUWN
      UYNUYLUWHUWMUYKVDYFYHUWEUVDXCUVEUUAUVFYKUVGUXGUYCUYGUYKGUWPUYMUYIUYKUYGVW
      CUYMVVDUYGUYHGUYGUYIGVVGUYGSUVHUYGUYHBUVIWMYTUVJYTUVKUYDUXFUXRUYBUYFMUWFU
      XFUWPUYCUVLUXHUXPUXRUYAUXHUXPUXRUXTUVNUVMUXNUWMUXIUXIUGUVOYOYAXFUVPUIXBUV
      QUVRUVSUWFUXFUWPUVTUWAUIUWBUWCUWLAEHUWDXS $.
      $( [12-Dec-2013] $) $( [3-Dec-2009] $)
  $}

  ${
    $d f x y z F $.  $d f x y z X $.
    filufint.1 $e |- X = U. F $.
    $( A filter is equal to the intersection of the ultrafilters containing
       it.  (Contributed by Jeff Hankins, 1-Jan-2010.) $)
    filufint $p |- ( F e. Fil -> |^| { f e. UFil | ( X = U. f /\ F C_ f ) } = F
    ) $=
      ( cfil wcel cuni wceq wss wa cufil wi wn w3a cvv 3ad2ant1 syl wne adantr
      c0 vx vy vz cv crab cint wral vex elintrab wrex cdif csn cun cfi undifabs
      cfv cfg uniexg syl5eqel difexg unisng 3syl eqcomd uneq12d syl5eqr syl6eqr
      uniun snex unexg mpan2 fiuni cfbas wb fsubbas filusb ne0i ssun1 ssn0 mpan
      a1i elsni elssuni syl6sseqr reldisj 3ad2ant2 dfss4 biimpi sseq2d 3ad2ant3
      cin bitrd fillsb 3expd 3imp sylbid necon3bd 3exp com24 3imp1 ineq2 neeq1d
      syl5ibrcom expimpd sylan2i ralrimivv filfbas difeq2 syl6eq eqtr3d eqeltrd
      dif0 3expia com23 snfil syl2anc fbunfip mpbird mpbir2and eqid fgbas fgfil
      ex 3eqtrd filssufil simpl2 simprl eqtrd ssfii syl5ss fbssfg sstrd ufilfil
      simprr filesn disjdif simpr3 ssun2 simpr2 snidg sseldd filint 3exp2 imp32
      syl3anc syl5eqelr mtod jca31 reximdvai syl5 sseq2 syl5ibr con3rr3 adantrd
      mp2d ancld reximdv syl5com pm2.61d rexanali syl6ib con4d syl5bi ssrdv cab
      wal df-rab inteqi sseq2i ssintab bitr2i mpgbi eqssd ) BEFZCAUDZGZHZBUVNIZ
      JZAKUEZUFZBUVMUAUVTBUAUDZUVTFUVRUWAUVNFZLAKUGZUVMUWABFZUVRAUWAKUAUHUIUVMU
      WDUWCUVMUWDMZUVRUWBMZJZAKUJZUWCMUVMUWEUWHUVMUWEJUWACIZUWHUVMUWEUWIUWHUVMU
      WEUWINZCBCUWAUKZULZUMZUNUPZUQUPZGZHZUWOEFZUWHUWJCUWMGZUWNGZUWPUWJCBGZUWLG
      ZUMZUWSUWJCCUWKUMUXCCUWAUOUWJCUXAUWKUXBCUXAHUWJDVTUVMUWEUWKUXBHUWIUVMUXBU
      WKUVMCOFZUWKOFZUXBUWKHUVMCUXAODBEURUSZCUWAOUTZUWKOVAVBVCPVDVEBUWLVGVFUVMU
      WEUWSUWTHZUWIUVMUWMOFZUXHUVMUWLOFUXIUWKVHBUWLEOVIVJZUWMOVKQPUWJUWNVLFZUWT
      UWPHUWJUXKUWMTRZTUWNFMZUVMUWEUXKUXLUXMJVMZUWIUVMUXIUXNUXJUWMOVNQPUVMUWEUX
      LUWIUVMCBFZBTRZUXLBCDVOZBCVPBUWMIUXPUXLBUWLVQZBUWMVRVSVBPUWJUXMUBUDZUCUDZ
      WJZTRZUCUWLUGUBBUGZUWJUYBUBUCBUWLUXTUWLFUWJUXSBFZUXTUWKHZUYBUXTUWKWAUWJUY
      DUYEUYBUWJUYDJUYBUYEUXSUWKWJZTRZUVMUWEUWIUYDUYGUVMUYDUWIUWEUYGUVMUYDUWIUW
      EUYGLUVMUYDUWINZUWDUYFTUYHUYFTHZUXSUWAIZUWDUYHUYIUXSCUWKUKZIZUYJUYDUVMUYI
      UYLVMZUWIUYDUXSCIUYMUYDUXSUXACUXSBWBDWCUXSUWKCWDQWEUWIUVMUYLUYJVMUYDUWIUY
      KUWAUXSUWIUYKUWAHZUWACWFWGZWHWIWKUVMUYDUWIUYJUWDLUVMUYDUWIUYJUWDUXSUWABCD
      WLWMWNWOWPWQWRWSUYEUYAUYFTUXTUWKUXSWTXAXBXCXDXEUWJBVLFZUWLVLFZUXMUYCVMUVM
      UWEUYPUWIBXFPUWJUWLEFZUYQUWJUXEUWKTRZUYRUVMUWEUXEUWIUVMUXDUXEUXFUXGQPUVMU
      WEUWIUYSUVMUWIUWEUYSUVMUWIUWEUYSLUVMUWIJUWDUWKTUVMUWIUWKTHZUWDUVMUWIUYTNZ
      UWACBVUAUYKUWACUWIUVMUYNUYTUYOWEUYTUVMUYKCHUWIUYTUYKCTUKCUWKTCXGCXKXHWIXI
      UVMUWIUXOUYTUXQPXJXLWPYBXMWNUWKOXNXOUWLXFQUBUCBUWLXPXOXQXRZUWNUWTUWTXSXTQ
      YCUWJUXKUWRVUBUWNYAQUWJUWQUWRUWHLUWRUWPUVOHZUWOUVNIZJZAKUJUWJUWQJZUWHAUWO
      UWPUWPXSYDVUFVUEUWGAKUWJUWQUVNKFZVUEUWGLUWJUWQVUGNZVUEUWGVUHVUEJZUVPUVQUW
      FVUICUWPUVOUWJUWQVUGVUEYEVUHVUCVUDYFYGVUIBUWOUVNVUHBUWOIZVUEUWJUWQVUJVUGU
      WJBUWNUWOUWJBUWMUWNUXRUVMUWEUWMUWNIZUWIUVMUXIVUKUXJUWMOYHQZPYIUWJUXKUWNUW
      OIZVUBUWNYJZQYKPSVUHVUCVUDYMYKVUIUWBTUVNFZVUHVUOMZVUEVUGUWJVUPUWQVUGUVNEF
      ZVUPUVNYLZUVNYNQWISVUHVUCVUDUWBVUOLVUHVUCVUDUWBVUOVUHVUCVUDUWBNZJZTUWAUWK
      WJZUVNUWACYOVUTVUQUWBUWKUVNFVVAUVNFVUHVUQVUSVUGUWJVUQUWQVURWISVUHVUCVUDUW
      BYPVUTUWLUVNUWKVUTUWLUWOUVNVUHUWLUWOIVUSVUHUWLUWNUWOUWJUWQUWLUWNIZVUGUVMU
      WEVVBUWIUVMUWLUWMUWNUWLBYQVULYIPPVUHUXKVUMUWJUWQUXKVUGVUBPVUNQYKSVUHVUCVU
      DUWBYRYKVUHUWKUWLFZVUSUWJUWQVVCVUGUVMUWEVVCUWIUVMUXDUXEVVCUXFUXGUWKOYSVBP
      PSYTUWAUWKUVNUUAUUDUUEUUBUUCUUFUUGYBXLUUHUUIYBUUNXLUVMUWIMZUWHLUWEUVMUVRA
      KUJVVDUWHABCDYDVVDUVRUWGAKVVDUVRUWFVVDUVPUWFUVQUVPUWBUWIUWBUWIUVPUWAUVOIU
      WAUVNWBCUVOUWAUUJUUKUULUUMUUOUUPUUQSUURYBUVRUWBAKUUSUUTUVAUVBUVCBUVTIZUVM
      VUGUVRJZUVQLZVVEAVVEBVVFAUVDZUFZIVVGAUVEUVTVVIBUVSVVHUVRAKUVFUVGUVHVVFABU
      VIUVJVUGUVPUVQYMUVKVTUVL $.
      $( [14-Dec-2013] $) $( [1-Jan-2010] $)
  $}

  ${
    $d x y A $.  $d x y X $.  $d x y V $.
    $( Lemma for ~ fixufil and ~ uffixfr .  (Contributed by Mario Carneiro,
       12-Dec-2013.) $)
    uffix $p |- ( ( X e. V /\ A e. X ) -> ( { { A } , X } e. fBas /\
                  { x e. ~P X | A e. x } = ( filGen ` { { A } , X } ) ) ) $=
      ( vy wcel wa csn cfbas cv wceq cun wss cvv sylancr syl snssi adantl sylib
      cuni cpr cpw crab cfg cfv df-pr cfil c0 wne snex snnzg snfil unisn eqcomi
      filfbas extbas1 syl2anc syl5eqel wrex vex elpw simpl uniprg eqtr2d sseq2d
      ssequn1 syl5bb prid1 sseq1 rcla4ev wi intss1 sstr2 snidg cin intprg df-ss
      cint eleqtrd syl5com sylan9r rexlimdva impbid2 anbi12d wb eleq2 elrab a1i
      ssel eqid elfg 3bitr4d eqrdv jca ) DCFZBDFZGZBHZDUAZIFZBAJZFZADUBZUCZWSUD
      UEZKWQWSWRHZDHLZIWRDUFWPXGIFZWOWPXFIFZWRDMZXHWPXFUGFZXIWPWRNFZWRUHUIXKBUJ
      ZBDUKWRNULOXFUOPBDQZDXFWRXFTWRWRXMUMUNUPUQRURZWQEXDXEWQEJZXCFZBXPFZGZXPWS
      TZMZXAXPMZAWSUSZGZXPXDFZXPXEFZWQXQYAXRYCXQXPDMWQYAXPDEUTVAWQDXTXPWQXTWRDL
      ZDWQXLWOXTYGKXMWOWPVBZWRDNCVCOWQXJYGDKWPXJWOXNRZWRDVFSVDVEVGWQXRYCXRWRWSF
      WRXPMZYCWRDXMVHBXPQYBYJAWRWSXAWRXPVIVJOWQYBXRAWSXAWSFZYBWSVRZXPMZWQXRYKYL
      XAMYBYMVKXAWSVLYLXAXPVMPWQBYLFYMXRWQBWRYLWPBWRFWOBDVNRWQYLWRDVOZWRWQXLWOY
      LYNKXMYHWRDNCVPOWQXJYNWRKYIWRDVQSVDVSYLXPBWIVTWAWBWCWDYEXSWEWQXBXRAXPXCXA
      XPBWFWGWHWQWTYFYDWEXOAXPWSXTXTWJWKPWLWMWN $.
      $( [12-Dec-2013] $)

    $( The condition describing a fixed ultrafilter always produces an
       ultrafilter.  (Contributed by Jeff Hankins, 9-Dec-2009.)  (Revised by
       Mario Carneiro, 12-Dec-2013.) $)
    fixufil $p |- ( ( X e. V /\ A e. X ) -> { x e. ~P X | A e. x } e. UFil ) $=
      ( vy wcel wa cv cpw cfil cuni cdif wo wral wceq syl cun wss eleq2 elrab
      crab cufil csn cpr cfg cfv cfbas uffix simprd simpld fgfil eqeltrd undif2
      elpwi ssequn1 sylib syl5req eleq2d biimpac elun adantll ibar adantl difss
      elpw2g mpbiri ad2antrr biantrurd mpbid ralrimiva ssrab2 uniss ax-mp unipw
      orbi12d sseqtri a1i ssid elpwg anim1i sylibr elssuni eqssd difeq1d eleq1d
      wb pweqd syl6bb raleqbidv mpbird eqid isufil sylanbrc ) DCFZBDFZGZBAHZFZA
      DIZUAZJFEHZWTFZWTKZXALZWTFZMZEXCIZNZWTUBFWPWTBUCDUDZUEUFZJWPXIUGFZWTXJOZA
      BCDUHZUIWPXKXJJFWPXKXLXMUJXIUKPULWPXHXAWSFZBXAFZGZDXALZWSFZBXQFZGZMZEWSNW
      PYAEWSWPXNGZXOXSMZYAWOXNYCWNWOXNGBXAXQQZFZYCXNWOYEXNDYDBXNYDXADQZDXADUMXN
      XADRYFDOXADUNXADUOUPUQURUSBXAXQUTUPVAYBXOXPXSXTXNXOXPWFWPXNXOVBVCYBXRXSWN
      XRWOXNWNXRXQDRDXAVDXQDCVEVFVGVHVOVIVJWPXFYAEXGWSWPXCDWPXCDXCDRWPXCWSKZDWT
      WSRXCYGRWRAWSVKWTWSVLVMDVNVPVQWPDWTFZDXCRWPDWSFZWOGYHWNYIWOWNYIDDRDVRDDCV
      SVFVTWRWOADWSWQDBSTWADWTWBPWCZWGWPXBXPXEXTXBXPWFWPWRXOAXAWSWQXABSTVQWPXEX
      QWTFXTWPXDXQWTWPXCDXAYJWDWEWRXSAXQWSWQXQBSTWHVOWIWJEWTXCXCWKWLWM $.
      $( [12-Dec-2013] $) $( [9-Dec-2009] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x X $.
    uffixfr.1 $e |- X = U. F $.
    $( An ultrafilter is either fixed or free.  A fixed ultrafilter is called
       principal (generated by a single element ` A ` ), and a free ultrafilter
       is called nonprincipal (having empty intersection).  Note that free
       ultrafilters cannot be constructed.  (Contributed by Jeff Hankins,
       4-Dec-2009.)  (Revised by Mario Carneiro, 12-Dec-2013.) $)
    uffixfr $p |- ( ( F e. UFil /\ A e. |^| F ) ->
                    F = { x e. ~P X | A e. x } ) $=
      ( cufil wcel cint wa cpw crab cfil cuni wceq wss simpl syl sylanbrc a1i
      cv csn cpr cfg cfv ufilfil filusb adantr c0 wne emnfil intssuni syl6sseqr
      cfbas sselda uffix syl2anc simprd simpld fgfil eqeltrd elpwg mpbiri eleq2
      3syl ssid elrab elssuni ssrab2 uniss ax-mp unipw sseqtri eqssd wral pwuni
      pweqi sseqtr4i elintg ibi adantl ssrab eqid ufilmax syl22anc ) CFGZBCHZGZ
      IZWEBATZGZADJZKZLGDWLMZNCWLOZCWLNWEWGPWHWLBUADUBZUCUDZLWHWOUMGZWLWPNZWHDC
      GZBDGZWQWRIWEWSWGWECLGZWSCUEZCDEUFQUGZWEWFDBWEWFCMZDWEXACUHUIWFXDOXBCUJCU
      KVDEULUNZABCDUOUPZUQWHWQWPLGWHWQWRXFURWOUSQUTWHDWMWHDWLGZDWMOWHDWKGZWTXGW
      HWSXHXCWSXHDDODVEDDCVAVBQXEWJWTADWKWIDBVCVFRDWLVGQWMDOWHWMWKMZDWLWKOWMXIO
      WJAWKVHWLWKVIVJDVKVLSVMWHCWKOZWJACVNZWNXJWHCXDJWKCVODXDEVPVQSWGXKWEWGXKAB
      CWFVRVSVTWJAWKCWARCWLDWMEWMWBWCWD $.
      $( [12-Dec-2013] $) $( [4-Dec-2009] $)
  $}

  ${
    $d x y F $.  $d x y X $.
    uffinfix.1 $e |- X = U. F $.
    $( An ultrafilter containing a finite element is fixed.  (Contributed by
       Jeff Hankins, 5-Dec-2009.) $)
    uffinfix $p |- ( ( F e. UFil /\ S e. F /\ S e. Fin ) ->
                     E. x e. X F = { y e. ~P X | x e. y } ) $=
      ( cufil wcel cfn w3a cv cint wex cpw crab wceq wrex cfbas c0 wne cfil syl
      ufilfil filfbas fbfinnfr n0 sylib syl3an1 wi wa cuni wss fbasne0 intssuni
      3syl syl6sseqr sseld uffixfr ex jcad eximdv df-rex syl6ibr 3ad2ant1 mpd )
      DGHZCDHZCIHZJAKZDLZHZAMZDVIBKHBENOPZAEQZVFDRHZVGVHVLVFDUAHVODUCDUDUBZVOVG
      VHJVJSTVLCDUEAVJUFUGUHVFVGVLVNUIVHVFVLVIEHZVMUJZAMVNVFVKVRAVFVKVQVMVFVJEV
      IVFVJDUKZEVFVODSTVJVSULVPDUMDUNUOFUPUQVFVKVMBVIDEFURUSUTVAVMAEVBVCVDVE $.
      $( [13-Dec-2013] $) $( [5-Dec-2009] $)
  $}

  ${
    $d f u v x y z A $.  $d f u v x y z B $.  $d f u v x y z X $.
    $( An infinite subset is contained in a free ultrafilter.  (Contributed by
       Jeff Hankins, 6-Dec-2009.) $)
    ufinffr $p |- ( ( X e. B /\ A C_ X /\ om ~<_ A ) -> E. f e. UFil ( X = U. f
    /\ A e. f /\ |^| f = (/) ) ) $=
      ( vx vy wcel wss com wbr w3a cv cdif cfn cuni wceq wa cufil c0 wn domnsym
      cdom crab wrex cint cfil csdm isfinite1b nsyl rcfpfil syl3an3 simpld eqid
      cpw filssufil syl simprd eqeq1d biimprd elpw2g biimpar 3adant3 a1i difeq2
      0fin difid syl6eq eleq1d elrab sylanbrc ssel syl5com intss csn vex eldifn
      snid mt2 elinti mtoi difss simp2 syl5ss wb 3ad2ant1 mpbird snfi wi notbii
      eldif iman bitr4i anbi2i bitri pm3.35 sylbi ssriv ssfi mp2an nsyl3 eq0rdv
      sseq2d syl5ib ss0 syl6 jcad anim12d 3anass syl6ibr reximdv mpd ) DBGZADHZ
      IAUBJZKZAELZMZNGZEDUNZUCZOZCLZOZPZXTYBHZQZCRUDZDYCPZAYBGZYBUEZSPZKZCRUDXO
      XTUFGZYGXOYMDYAPZXNXLXMANGZTYMYNQXNAIUGJYOIAUAAUHUIEABDUJUKZULCXTYAYAUMUO
      UPXOYFYLCRXOYFYHYIYKQZQYLXOYDYHYEYQXOYHYDXODYAYCXOYMYNYPUQURUSXOYEYIYKXOA
      XTGZYEYIXOAXSGZSNGZYRXLXMYSXNXLYSXMADBUTVAVBYTXOVEVCXRYTEAXSXPAPZXQSNUUAX
      QAAMSXPAAVDAVFVGVHVIVJXTYBAVKVLXOYEYJSHZYKYEYJXTUEZHXOUUBXTYBVMXOUUCSYJXO
      FUUCFLZUUCGZAUUDVNZMZXTGZXOUUEUUHUUDUUGGZUUIUUDUUFGUUDFVOVQUUDAUUFVPVRUUD
      XTUUGVSVTXOUUGXSGZAUUGMZNGZUUHXOUUJUUGDHZXOUUGADAUUFWAXLXMXNWBWCXLXMUUJUU
      MWDXNUUGDBUTWEWFUULXOUUFNGUUKUUFHUULUUDWGEUUKUUFXPUUKGZXPAGZUUOXPUUFGZWHZ
      QZUUPUUNUUOXPUUGGZTZQUURXPAUUGWJUUTUUQUUOUUTUUOUUPTQZTUUQUUSUVAXPAUUFWJWI
      UUOUUPWKWLWMWNUUOUUPWOWPWQUUFUUKWRWSVCXRUULEUUGXSXPUUGPXQUUKNXPUUGAVDVHVI
      VJWTXAXBXCYJXDXEXFXGYHYIYKXHXIXJXK $.
      $( [4-Dec-2013] $) $( [6-Dec-2009] $)
  $}

  ${
    $d f u v x y z A $.  $d f u v x y z F $.  $d f u v x y z X $.
    $( Any infinite set has an ultrafilter on it whose elements are of the same
       cardinality as the set.  Any such ultrafilter is necessarily free.
       (Contributed by Jeff Hankins, 7-Dec-2009.)  (The proof was shortened by
       Mario Carneiro, 14-Dec-2013.) $)
    ufilen $p |- ( ( X e. A /\ om ~<_ X ) -> E. f e. UFil ( X = U. f /\ A. x e.
    f x ~~ X ) ) $=
      ( vy wcel cdom wbr wa cv cdif csdm cuni wceq wss cufil wrex cfil syl wi
      com cpw crab cen wral csdfil simpld filssufil simprd eqeq1d anbi1d adantr
      eqid wb wn simplll elssuni ad2antll simprl sseqtr4d cfbas ufilfil filfbas
      adantl simpr fbncp syl2an w3a difss elpw2g mpbiri simp2 dfss4 sylib simp3
      3ad2ant1 eqbrtrd difeq2 breq1d elrab sylanbrc ssel syl5com 3expa impancom
      con3d syl21anc cvv vex ax-mp bren2 simplbi2 3syl syld expr com23 ralrimdv
      ssdomg imdistanda sylbird reximdva mpd ) DBFZUADGHZIZDEJZKZDLHZEDUBZUCZMZ
      CJZMZNZXJXLOZIZCPQZDXMNZAJZDUDHZAXLUEZIZCPQXEXJRFZXQXEYCDXKNZEBDUFZUGCXJX
      KXKUMUHSXEXPYBCPXEXLPFZIZXPXRXOIZYBXEYHXPUNYFXEXRXNXOXEDXKXMXEYCYDYEUIUJU
      KULYGXRXOYAYGXRIZXOXTAXLYIXSXLFZXOXTYGXRYJXOXTTYGXRYJIZIZXOXSDLHZUOZXTYLX
      CXSDOZDXSKZXLFZUOZXOYNTXCXDYFYKUPYLXSXMDYJXSXMOYGXRXSXLUQURYGXRYJUSUTZYGX
      LVAFZYJYRYKYFYTXEYFXLRFYTXLVBXLVCSVDXRYJVEXSDXLVFVGXCYOIZXOYRYNUUAXOIYMYQ
      UUAYMXOYQXCYOYMXOYQTXCYOYMVHZYPXJFZXOYQUUBYPXIFZDYPKZDLHZUUCXCYOUUDYMXCUU
      DYPDODXSVIYPDBVJVKVPUUBUUEXSDLUUBYOUUEXSNXCYOYMVLXSDVMVNXCYOYMVOVQXHUUFEY
      PXIXFYPNXGUUEDLXFYPDVRVSVTWAXJXLYPWBWCWDWEWFWEWGYLYOXSDGHZYNXTTYSXSWHFYOU
      UGTAWIXSDWHWRWJXTUUGYNXSDWKWLWMWNWOWPWQWSWTXAXB $.
      $( [14-Dec-2013] $) $( [7-Dec-2009] $)
  $}

  ${
    $d x F $.  $d x J $.  $d x X $.
    ufildr.1 $e |- J = ( F u. { (/) } ) $.
    ufildr.2 $e |- X = U. F $.
    $( An ultrafilter gives rise to a connected door topology.  (Contributed by
       Jeff Hankins, 6-Dec-2009.)  (Revised by Mario Carneiro, 14-Dec-2013.) $)
    ufildr $p |- ( F e. UFil -> ( J u. ( Clsd ` J ) ) = ~P X ) $=
      ( vx cufil wcel ccld cfv cun wo wss wi cuni c0 a1i ctop ex sseld cv uniun
      cpw elssuni csn 0ex uneq2i un0 3eqtri unieqi 3eqtr4ri syl6sseqr cfil ccon
      unisn ufilfil filcon contop 3syl syl5eqel cldss jaod wa cdif ufilss ssun1
      syl sseqtr4i wb iscld2 sylan sylibrd orim12d mpd impbid elun elpw 3bitr4g
      vex eqrdv ) AGHZFBBIJZKZCUCZWAFUAZBHZWEWBHZLZWECMZWEWCHWEWDHWAWHWIWAWFWIW
      GWFWINWAWFWEBOZCWEBUDAPUEZKZOZAOZWJCWMWNWKOZKWNPKWNAWKUBWOPWNPUFUOUGWNUHU
      IBWLDUJEUKZULQWABRHZWGWINWABWLRDWAAUMHWLUNHWLRHAUPAUQWLURUSUTZWQWGWIWEBCW
      PVASVGVBWAWIWHWAWIVCZWEAHZCWEVDZAHZLWHWEACEVEWSWTWFXBWGWSABWEABMWSAWLBAWK
      VFDVHQZTWSXBXABHZWGWSABXAXCTWAWQWIWGXDVIWRWEBCWPVJVKVLVMVNSVOWEBWBVPWECFV
      SVQVRVT $.
      $( [14-Dec-2013] $) $( [6-Dec-2009] $)
  $}

