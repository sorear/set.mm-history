$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Basic topological facts
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ntrcmp.1 $e |- X = U. J $.
    $( An interior of a complement is the complement of the closure.  This set
       is also known as the exterior of ` A ` .  (Contributed by Jeff Hankins,
       31-Aug-2009.) $)
    ntrcmp $p |- ( ( J e. Top /\ A C_ X ) -> ( ( int ` J ) ` ( X \ A ) ) = ( X
    \ ( ( cls ` J ) ` A ) ) ) $=
      ( ctop wcel wss wa cdif cnt cfv ccl wceq difss ntrval2 mpan2 adantr simpr
      dfss4 sylib fveq2d difeq2d eqtrd ) BEFZACGZHZCAIZBJKKZCCUGIZBLKZKZIZCAUJK
      ZIUDUHULMZUEUDUGCGUNCANUGBCDOPQUFUKUMCUFUIAUJUFUEUIAMUDUERACSTUAUBUC $.
      $( [31-Aug-2009] $)
  $}

  ${
    clscmp.1 $e |- X = U. J $.
    $( A closure of a complement is the complement of the interior.
       (Contributed by Jeff Hankins, 31-Aug-2009.) $)
    clscmp $p |- ( ( J e. Top /\ A C_ X ) -> ( ( cls ` J ) ` ( X \ A ) ) = ( X
    \ ( ( int ` J ) ` A ) ) ) $=
      ( ctop wcel wss wa cdif ccl cfv cnt wceq difss clsval2 mpan2 adantr simpr
      dfss4 sylib fveq2d difeq2d eqtrd ) BEFZACGZHZCAIZBJKKZCCUGIZBLKZKZIZCAUJK
      ZIUDUHULMZUEUDUGCGUNCANUGBCDOPQUFUKUMCUFUIAUJUFUEUIAMUDUERACSTUAUBUC $.
      $( [31-Aug-2009] $)
  $}

  ${
    topbnd.1 $e |- X = U. J $.
    $( Two equivalent expressions for the boundary of a topology.  (Contributed
       by Jeff Hankins, 23-Sep-2009.) $)
    topbnd $p |- ( ( J e. Top /\ A C_ X ) -> ( ( ( cls ` J ) ` A ) i^i ( ( cls
    ` J ) ` ( X \ A ) ) ) = ( ( ( cls ` J ) ` A ) \ ( ( int ` J ) ` A ) ) ) $=
      ( ctop wcel wss wa ccl cfv cdif cin cnt clscmp ineq2d indif2 syl6eq df-ss
      wceq clsss3 sylib difeq1d eqtrd ) BEFACGHZABIJZJZCAKUEJZLZUFCLZABMJJZKZUF
      UJKUDUHUFCUJKZLUKUDUGULUFABCDNOUFCUJPQUDUIUFUJUDUFCGUIUFSABCDTUFCRUAUBUC
      $.
      $( [23-Sep-2009] $)
  $}

  ${
    opnbnd.1 $e |- X = U. J $.
    $( A set is open iff it is disjoint from its boundary.  (Contributed by
       Jeff Hankins, 23-Sep-2009.) $)
    opnbnd $p |- ( ( J e. Top /\ A C_ X ) -> ( A e. J <-> ( A i^i ( ( ( cls ` J
    ) ` A ) i^i ( ( cls ` J ) ` ( X \ A ) ) ) ) = (/) ) ) $=
      ( ctop wcel wss wa cnt cfv wceq ccl cdif cin disjdif a1i eqeq1d syl5ibcom
      c0 ineq1 ntrss2 adantr inssdif0 sscls df-ss sylib eqcomd eqimss syl sylan
      sstr sylan2br eqssd ex impbid isopn3 topbnd ineq2d 3bitr4d ) BEFACGHZABIJ
      JZAKZAABLJZJZVAMZNZSKZABFAVDCAMVCJNZNZSKUTVBVGUTVAVENZSKZVBVGVKUTVAVDOPVB
      VJVFSVAAVETQRUTVGVBUTVGHVAAUTVAAGVGABCDUAUBVGUTAVDNZVAGZAVAGZAVDVAUCUTAVL
      GZVMVNUTAVLKVOUTVLAUTAVDGVLAKABCDUDAVDUEUFUGAVLUHUIAVLVAUKUJULUMUNUOABCDU
      PUTVIVFSUTVHVEAABCDUQURQUS $.
      $( [23-Sep-2009] $)

    $( A set is closed iff it contains its boundary.  (Contributed by Jeff
       Hankins, 1-Oct-2009.) $)
    cldbnd $p |- ( ( J e. Top /\ A C_ X ) -> ( A e. ( Clsd ` J ) <-> ( ( ( cls
    ` J ) ` A ) i^i ( ( cls ` J ) ` ( X \ A ) ) ) C_ A ) ) $=
      ( ctop wcel wss wa cfv cdif cin c0 adantl incom ex sylbi ineq2d wb adantr
      wceq ccld ccl iscld3 eqimss syl6bi ssinss1 syl6 sslin disjdif eqtri sseq0
      a1i syl2anc dfss4 fveq2 eqcomd syl5eq eqeq1d difss opnbnd mpan2 bitr4d wi
      opncld eleq1 sylibd sylbid syld impbid ) BEFZACGZHZABUAIZFZABUBIZIZCAJZVO
      IZKZAGZVLVNVPAGZVTVLVNVPATWAABCDUCVPAUDUEVPVRAUFUGVLVTVQVSKZLTZVNVLVTWCVL
      VTHZWBVQAKZGZWELTZWCVTWFVLVSAVQUHMWGWDWEAVQKLVQANACUIUJULWBWEUKUMOVLWCVQB
      FZVNVLWCVQVRCVQJZVOIZKZKZLTZWHVLWBWLLVLVSWKVQVLVSVRVPKWKVPVRNVLVPWJVRVKVP
      WJTZVJVKWIATZWNACUNZWOWJVPWIAVOUOUPPMQUQQURVJWHWMRZVKVJVQCGWQCAUSVQBCDUTV
      ASVBVLWHWIVMFZVNVJWHWRVCVKVJWHWRVQBCDVDOSVKWRVNRZVJVKWOWSWPWIAVMVEPMVFVGV
      HVI $.
      $( [1-Oct-2009] $)
  $}

  ${
    $d o J $.  $d o O $.  $d o X $.
    ntruni.1 $e |- X = U. J $.
    $( A union of interiors is a subset of the interior of the union.  The
       reverse inclusion may not hold.  (Contributed by Jeff Hankins,
       31-Aug-2009.) $)
    ntruni $p |- ( ( J e. Top /\ O C_ ~P X ) -> U_ o e. O ( ( int ` J ) ` o
    ) C_ ( ( int ` J ) ` U. O ) ) $=
      ( ctop wcel cpw wss wa cv cnt cfv cuni wral ciun elssuni wi sspwuni ntrss
      3expia sylan2b syl5 ralrimiv iunss sylibr ) BFGZCDHIZJZAKZBLMZMZCNZUKMZIZ
      ACOACULPUNIUIUOACUJCGUJUMIZUIUOUJCQUHUGUMDIZUPUORCDSUGUQUPUOUMUJBDETUAUBU
      CUDACULUNUEUF $.
      $( [31-Aug-2009] $)
  $}

  ${
    clsun.1 $e |- X = U. J $.
    $( A pairwise union of closures is the closure of the union.  (Contributed
       by Jeff Hankins, 31-Aug-2009.) $)
    clsun $p |- ( ( J e. Top /\ A C_ X /\ B C_ X ) -> ( ( cls ` J ) ` ( A u. B
    ) ) = ( ( ( cls ` J ) ` A ) u. ( ( cls ` J ) ` B ) ) ) $=
      ( wcel wss cun cfv cdif difundi wceq difss wa unss ntrcmp syl2anc 3adant3
      cin 3adant2 w3a ccl cnt fveq2i ntrin mp3an23 3ad2ant1 syl5eq simp1 biimpi
      ctop 3adant1 ineq12d syl6eqr 3eqtr3d ccld clscld cldss dfss4 sylib clsss3
      difeq2d jca bitri ) CUKFZADGZBDGZUAZDDABHZCUBIZIZJZJZDDAVJIZBVJIZHZJZJZVK
      VPVHVLVQDVHDVIJZCUCIZIZDAJZVTIZDBJZVTIZSZVLVQVHWAWBWDSZVTIZWFVSWGVTDABKUD
      VEVFWHWFLZVGVEWBDGWDDGWIDAMDBMWBWDCDEUEUFUGUHVHVEVIDGZWAVLLVEVFVGUIZVFVGW
      JVEVFVGNWJABDOUJULZVICDEPQVHWFDVNJZDVOJZSVQVHWCWMWEWNVEVFWCWMLVGACDEPRVEV
      GWEWNLVFBCDEPTUMDVNVOKUNUOVBVHVKDGZVMVKLVHVEVKCUPIFZWOWKVHVEWJWPWKWLVICDE
      UQQVKCDEURQVKDUSUTVHVNDGZVODGZNZVRVPLZVHWQWRVEVFWQVGACDEVARVEVGWRVFBCDEVA
      TVCWSVPDGWTVNVODOVPDUSVDUTUO $.
      $( [31-Aug-2009] $)
  $}

  ${
    $d c C $.  $d c J $.  $d c X $.
    clsint2.1 $e |- X = U. J $.
    $( The closure of an intersection is a subset of the intersection of the
       closures.  (Contributed by Jeff Hankins, 31-Aug-2009.) $)
    clsint2 $p |- ( ( J e. Top /\ C C_ ~P X ) -> ( ( cls ` J ) ` |^| C ) C_
    |^|_ c e. C ( ( cls ` J ) ` c ) ) $=
      ( ctop wcel cpw wss wa cint ccl cfv cv wral ciin cuni wi sspwuni elssuni
      sstr2 syl adantl intss1 clsss syl3an3 3com23 3expia syld ex com23 sylan2b
      imp ralrimiv ssiin sylibr ) BFGZACHIZJZAKZBLMZMZDNZVAMZIZDAOVBDAVDPIUSVED
      AURUQAQZCIZVCAGZVERZACSUQVGVIUQVHVGVEUQVHVGVERUQVHJVGVCCIZVEVHVGVJRZUQVHV
      CVFIVKVCATVCVFCUAUBUCUQVHVJVEUQVJVHVEVHUQVJUTVCIVEVCAUDVCUTBCEUEUFUGUHUIU
      JUKUMULUNDAVDVBUOUP $.
      $( [31-Aug-2009] $)
  $}

  ${
    $d c o A $.  $d c o J $.  $d c o X $.
    opnregcld.1 $e |- X = U. J $.
    $( A set is regularly closed iff it is the closure of some open set.
       (Contributed by Jeff Hankins, 27-Sep-2009.) $)
    opnregcld $p |- ( ( J e. Top /\ A C_ X ) -> ( ( ( cls ` J ) ` ( ( int ` J )
    ` A ) ) = A <-> E. o e. J A = ( ( cls ` J ) ` o ) ) ) $=
      ( ctop wcel wss wa cnt cfv wceq cv wrex ntropn fveq2 syldan clsss syl3anc
      ccl eqcom biimpi eqeq2d rcla4ev syl2an simpl eltopss clsss3 ntrss2 clsidm
      ex sseqtrd ntrss3 simpr sscls ssntr syl22anc eqssd adantlr fveq2d eqeq12d
      id syl5ibrcom rexlimdva impbid ) CFGZADHZIZACJKZKZCTKZKZALZABMZVKKZLZBCNZ
      VHVMVQVHVJCGAVLLZVQVMACDEOVMVRVLAUAUBVPVRBVJCVNVJLVOVLAVNVJVKPUCUDUEUKVHV
      PVMBCVHVNCGZIVMVPVOVIKZVKKZVOLZVFVSWBVGVFVSIZWAVOWCWAVOVKKZVOWCVFVODHZVTV
      OHZWAWDHVFVSUFZVFVSVNDHZWEVNCDEUGZVNCDEUHQZVFVSWEWFWJVOCDEUIQVOVTCDERSVFV
      SWHWDVOLWIVNCDEUJQULWCVFVTDHZVNVTHZVOWAHWGVFVSWEWKWJVOCDEUMQWCVFWEVSVNVOH
      ZWLWGWJVFVSUNVFVSWHWMWIVNCDEUOQVOCVNDEUPUQVTVNCDERSURUSVPVLWAAVOVPVJVTVKA
      VOVIPUTVPVBVAVCVDVE $.
      $( [27-Sep-2009] $)

    $( A set if regularly open iff it is the interior of some closed set.
       (Contributed by Jeff Hankins, 27-Sep-2009.) $)
    cldregopn $p |- ( ( J e. Top /\ A C_ X ) -> ( ( ( int ` J ) ` ( ( cls ` J )
    ` A ) ) = A <-> E. c e. ( Clsd ` J ) A = ( ( int ` J ) ` c ) ) ) $=
      ( ctop wcel wss wa ccl cfv cnt wceq ccld wrex fveq2 syldan ntrss syl3anc
      cv clscld eqcom biimpi eqeq2d rcla4ev syl2an ex simpl cldss ntrss2 clsss2
      mpd3an3 ntridm ntrss3 clsss3 sscls eqsstr3d adantlr id eqeq12d syl5ibrcom
      eqssd fveq2d rexlimdva impbid ) BFGZACHZIZABJKZKZBLKZKZAMZADTZVKKZMZDBNKZ
      OZVHVMVRVHVJVQGAVLMZVRVMABCEUAVMVSVLAUBUCVPVSDVJVQVNVJMVOVLAVNVJVKPUDUEUF
      UGVHVPVMDVQVHVNVQGZIVMVPVOVIKZVKKZVOMZVFVTWCVGVFVTIZWBVOWDVFVNCHZWAVNHZWB
      VOHVFVTUHZVNBCEUIZVFVTVOVNHZWFVFVTWEWIWHVNBCEUJQVNVOBCEUKULVNWABCERSWDVOV
      OVKKZWBVFVTWEWJVOMWHVNBCEUMQWDVFWACHZVOWAHZWJWBHWGVFVTVOCHZWKVFVTWEWMWHVN
      BCEUNQZVOBCEUOQVFVTWMWLWNVOBCEUPQWAVOBCERSUQVBURVPVLWBAVOVPVJWAVKAVOVIPVC
      VPUSUTVAVDVE $.
      $( [27-Sep-2009] $)
  $}

  $( Two neighborhoods intersect to form a neighborhood of the intersection.
     (Contributed by Jeff Hankins, 31-Aug-2009.) $)
  neiin $p |- ( ( J e. Top /\ M e. ( ( nei ` J ) ` A ) /\ N e. ( ( nei ` J ) `
  B ) ) -> ( M i^i N ) e. ( ( nei ` J ) ` ( A i^i B ) ) ) $=
    ( cfv cin wss wa simpr wb simpl neiss2 neii1 neiint syl3anc ssinss1 3adant3
    wcel syl ctop cnei w3a cnt cuni eqid mpbid 3adant2 inss2 sstr mpan jca ssin
    sylib wceq simp1 ntrin sseqtr4d mpbird ) CUASZDACUBFZFSZEBVAFSZUCZDEGZABGZV
    AFSZVFVECUDFZFZHZVDVFDVHFZEVHFZGZVIVDVFVKHZVFVLHZIVFVMHVDVNVOUTVBVNVCUTVBIZ
    AVKHZVNVPVBVQUTVBJVPUTACUEZHZDVRHZVBVQKUTVBLZACDVRVRUFZMZACDVRWBNZACDVRWBOP
    UGABVKQTRVDBVLHZVOUTVCWEVBUTVCIZVCWEUTVCJWFUTBVRHEVRHZVCWEKUTVCLBCEVRWBMBCE
    VRWBNZBCEVRWBOPUGUHVFBHWEVOABUIVFBVLUJUKTULVFVKVLUMUNVDUTVTWGVIVMUOUTVBVCUP
    UTVBVTVCWDRUTVCWGVBWHUHDECVRWBUQPURUTVBVGVJKZVCVPUTVFVRHZVEVRHZWIWAVPVSWJWC
    ABVRQTVPVTWKWDDEVRQTVFCVEVRWBOPRUS $.
    $( [31-Aug-2009] $)

  ${
    $d c o x y F $.  $d c o x y J $.  $d c o x y K $.  $d c o x y X $.
    $d c o x y Y $.
    cncls.1 $e |- X = U. J $.
    cncls.2 $e |- Y = U. K $.
    $( Continuity in terms of closure.  (Contributed by Jeff Hankins,
       1-Oct-2009.) $)
    cncls $p |- ( ( J e. Top /\ K e. Top /\ F : X --> Y ) -> ( F e. ( J Cn K )
    <-> A. x ( x C_ X -> ( F " ( ( cls ` J ) ` x ) ) C_ ( ( cls ` K ) ` ( F " x
    ) ) ) ) ) $=
      ( vc wcel cima cfv wa wss wb 3ad2ant3 ad2antrr adantr syl2anc cvv ctop wf
      w3a ccn co ccnv cv ccld wral ccl wi iscncl 3adant3 ibar wfun ffun simpll1
      wal simp2 crn imassrn frn syl5ss clscld wceq imaeq2 eleq1d rcla4v syl imp
      simpll2 sscls cdm sseq2 biimpar sylan 3ad2antl3 funimass3 mpbid funimass2
      fdm clsss2 syl3anc exp31 com23 alrimdv wfn ffn fndm cnvimass mpbii uniexg
      3syl cuni syl5eqel adantl simpl ssexg 3adant2 sseq1 fveq2 imaeq2d sseq12d
      fveq2d imbi12d cla4gv mpid cldss 3ad2antl2 ssid sylancl clsss simpr sstrd
      a1i syl5sseq clsss3 sseqtr4d iscld4 mpbird ralrimdv impbid 3bitr2d ) CUAJ
      ZDUAJZEFBUBZUCZBCDUDUEJZYFBUFZIUGZKZCUHLZJZIDUHLZUIZMZYOAUGZENZBYQCUJLZLZ
      KZBYQKZDUJLZLZNZUKZAURZYDYEYHYPOYFIBCDEFGHULUMYFYDYOYPOYEYFYOUNPYGYOUUGYG
      YOUUFAYGYRYOUUEYGYRYOUUEYGYRMZYOMZBUOZYTYIUUDKZNZUUEYGUUJYRYOYFYDUUJYEEFB
      UPPZQZUUIYDUUKYLJZYQUUKNZUULYDYEYFYRYOUQUUHYOUUOUUHUUDYNJZYOUUOUKUUHYEUUB
      FNZUUQYGYEYRYDYEYFUSZRYGUURYRYFYDUURYEYFUUBBUTFBYQVAEFBVBVCPZRUUBDFHVDSYM
      UUOIUUDYNYJUUDVEYKUUKYLYJUUDYIVFVGVHVIVJUUIUUBUUDNZUUPUUIYEUURUVAYDYEYFYR
      YOVKYGUURYRYOUUTQUUBDFHVLSUUIUUJYQBVMZNZUVAUUPOUUNUUHUVCYOYFYDYRUVCYEYFUV
      BEVEZYRUVCEFBWAZUVDUVCYRUVBEYQVNVOVPVQRYQUUDBVRSVSUUKYQCEGWBWCYTUUDBVTSWD
      WEWFYGUUGYMIYNYGYJYNJZUUGYMYGUVFUUGYMYGUVFMZUUGMZYMYKYSLZYKNZUVHBUVIKZYJN
      ZUVJUVHUVKBYKKZUUCLZYJUVGUUGUVKUVNNZUVGUUGYKENZUVOYGUVPUVFYFYDUVPYEYFBEWG
      ZUVDUVPEFBWHZEBWIZUVDYKUVBNZUVPBYJWJZUVBEYKVNZWKWMPZRUVGYKTJZUUGUVPUVOUKZ
      UKYGUWDUVFYDYFUWDYEYDETJZYFUWDYDECWNTGCUAWLWOUWFYFMZUVPUWFUWDUWGUVTUVPUWA
      YFUVTUVPOZUWFYFUVQUVDUWHUVRUVSUWBWMWPWKUWFYFWQYKETWRSVPWSRUUFUWEAYKTYQYKV
      EZYRUVPUUEUVOYQYKEWTUWIUUAUVKUUDUVNUWIYTUVIBYQYKYSXAXBUWIUUBUVMUUCYQYKBVF
      XDXCXEXFVIXGVJUVGUVNYJNUUGUVGUVNYJUUCLZYJUVGYEYJFNZUVMYJNZUVNUWJNYGYEUVFU
      USRZYEYDUVFUWKYFYJDFHXHXIUVGUUJYKYKNUWLYGUUJUVFUUMRYKXJYKYJBVTXKYJUVMDFHX
      LWCUVGYEUVFYJYJNZUWJYJNUWMYGUVFXMUWNUVGYJXJXOYJYJDFHWBWCXNRXNUVHUUJUVIUVB
      NUVLUVJOYGUUJUVFUUGUUMQUVHUVIEUVBUVHYDUVPUVIENYDYEYFUVFUUGUQZYGUVPUVFUUGY
      FYDUVPYEYFUVBYKEUWAUVEXPPQYKCEGXQSYGUVDUVFUUGYFYDUVDYEUVEPQXRUVIYJBVRSVSU
      VHYDUVPYMUVJOUWOYGUVPUVFUUGUWCQYKCEGXSSXTWDWEYAYBYC $.
      $( [1-Oct-2009] $)

    $( Continuity in terms of interior.  (Contributed by Jeff Hankins,
       2-Oct-2009.) $)
    cnntr $p |- ( ( J e. Top /\ K e. Top /\ F : X --> Y )
       -> ( F e. ( J Cn K ) <-> A. y ( y C_ Y -> ( `' F " ( ( int ` K ) ` y ) )
            C_ ( ( int ` J ) ` ( `' F " y ) ) ) ) ) $=
      ( vo wcel cima wa wss cfv wi wb 3ad2ant3 adantr wceq 3ad2antl2 wf w3a ccn
      ctop co ccnv cv wral cnt wal iscn 3adant3 ibar simp1 wfn cdm ffn cnvimass
      fndm sseq2 mpbii 3syl ntropn imaeq2 eleq1d rcla4v syl com23 imp32 adantrl
      ex ntrss2 imass2 ssntr syl22anc exp32 alrimdv fdm syl5sseq syl2anc simprr
      simp2 eltopss isopn3 mpbid imaeq2d sseq1 fveq2 fveq2d sseq12d cla4gv mpid
      imbi12d adantl eqsstr3d eqssd mpbird ralrimdv impbid 3bitr2d ) CUDJZDUDJZ
      EFBUAZUBZBCDUCUEJZXCBUFZIUGZKZCJZIDUHZLZXJAUGZFMZXFXLDUINZNZKZXFXLKZCUINZ
      NZMZOZAUJZXAXBXEXKPXCIBCDEFGHUKULXCXAXJXKPXBXCXJUMQXDXJYBXDXJYAAXDXJXMXTX
      DXJXMLZLZXAXQEMZXPCJZXPXQMZXTXDXAYCXAXBXCUNZRXDYEYCXCXAYEXBXCBEUOBUPZESZY
      EEFBUQEBUSYJXQYIMYEBXLURYIEXQUTVAVBQRXDXJXMYFXDXMXJYFXDXMXJYFOZXDXMLXODJZ
      YKXBXAXMYLXCXLDFHVCTXIYFIXODXGXOSXHXPCXGXOXFVDVEVFVGVKVHVIYDXOXLMZYGXDXMY
      MXJXBXAXMYMXCXLDFHVLTVJXOXLXFVMVGXQCXPEGVNVOVPVQXDYBXIIDXDYBXGDJZXIXDYBYN
      LZLZXIXHXRNZXHSZYPYQXHYPXAXHEMZYQXHMXDXAYOYHRZXDYSYOXCXAYSXBXCYIXHEBXGURE
      FBVRVSQRZXHCEGVLVTYPXHXFXGXNNZKZYQYPUUBXGXFYPYNUUBXGSZXDYBYNWAYPXBXGFMZYN
      UUDPXDXBYOXAXBXCWBRXDYNUUEYBXBXAYNUUEXCXGDFHWCTZVJXGDFHWDVTWEWFXDYBYNUUCY
      QMZXDYNYBUUGXDYNYBUUGOXDYNLYBUUEUUGUUFYNYBUUEUUGOZOXDYAUUHAXGDXLXGSZXMUUE
      XTUUGXLXGFWGUUIXPUUCXSYQUUIXOUUBXFXLXGXNWHWFUUIXQXHXRXLXGXFVDWIWJWMWKWNWL
      VKVHVIWOWPYPXAYSXIYRPYTUUAXHCEGWDVTWQVPWRWSWT $.
      $( [2-Oct-2009] $)
  $}

  $( Homeomorphisms preserve closedness.  (Contributed by Jeff Hankins,
     3-Jul-2009.) $)
  hmeoclda $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Homeo K ) ) /\ S e. (
  Clsd ` J ) ) -> ( F " S ) e. ( Clsd ` K ) ) $=
    ( ctop wcel chomeosm co w3a ccld cfv wa cima ccnv imacnvcnv ccn simp2 simp1
    hmeobc biimp3a simprd 3jca cnclima sylan syl5eqelr ) CEFZDEFZBCDGHFZIZACJKF
    ZLBAMBNZNAMZDJKZBAOUIUGUFUKDCPHFZIUJULUMFUIUGUFUNUFUGUHQUFUGUHRUIBCDPHFZUNU
    FUGUHUOUNLBCDSTUAUBAUKDCUCUDUE $.
    $( [3-Jun-2014] $) $( [3-Jul-2009] $)

  $( Homeomorphisms preserve closedness.  (Contributed by Jeff Hankins,
     3-Jul-2009.) $)
  hmeocldb $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Homeo K ) ) /\ S e. (
  Clsd ` K ) ) -> ( `' F " S ) e. ( Clsd ` J ) ) $=
    ( ctop wcel chomeosm co w3a ccnv ccld cima simp2 simp1 cnvhmpha 3impia 3jca
    cfv hmeoclda sylan ) CEFZDEFZBCDGHFZIZUBUABJZDCGHFZIADKRFUEALCKRFUDUBUAUFUA
    UBUCMUAUBUCNUAUBUCUFBCDOPQAUEDCST $.
    $( [3-Jul-2009] $)

  ${
    $d a b c d f j k m n s t u v w x y z J $.
    $d a b c d f j k m n s t u v w x y z X $.
    alexsub.1 $e |- X = U. J $.
    $( Lemma for ~ alexsub .  A compact space has a subbase such that every
       cover taken from it has a finite subcover. $)
    alexsublem1 $p |- ( J e. Comp -> E. x ( J = ( topGen ` ( fi ` x ) ) /\ A. c
    e. ~P x ( X = U. c -> E. d e. ( ~P c i^i Fin ) X = U. d ) ) ) $=
      ( ccmp wcel cfi cfv ctg wceq cv cuni cpw cfn cin wral wa fveq2d wrex ctop
      wi wex cmptop fitop tgtop eqtr2d syl wss elpw cmpcov 3exp syl5bi ralrimiv
      vex fveq2 eqeq2d pweq raleqdv anbi12d cla4egv mp2and ) BGHZBBIJZKJZLZCDMZ
      NLZCEMNLEVHOPQUAZUCZDBOZRZBAMZIJZKJZLZVKDVNOZRZSZAUDVDBUBHZVGBUEWAVFBKJBW
      AVEBKBUFTBUGUHUIVDVKDVLVHVLHVHBUJZVDVKVHBDUPUKVDWBVIVJVHBCEFULUMUNUOVTVGV
      MSABGVNBLZVQVGVSVMWCVPVFBWCVOVEKVNBIUQTURWCVKDVRVLVNBUSUTVAVBVC $.
      $( [27-Jan-2010] $)

    $( Lemma for ~ alexsub .  Every subset of a base which has no finite
       subcover is a subset of a maximal such collection. $)
    alexsublem2 $p |- ( ( ( J = ( topGen ` ( fi ` x ) ) /\ A. c e. ~P x ( X =
    U. c -> E. d e. ( ~P c i^i Fin ) X = U. d ) /\ a e. ~P ( fi ` x ) ) /\ A. b
    e. ( ~P a i^i Fin ) -. X = U. b ) -> E. u e. ( { z e. ~P ( fi ` x ) | ( a
    C_ z /\ A. b e. ( ~P z i^i Fin ) -. X = U. b ) } u. { (/) } ) A. v e. ( { z
    e. ~P ( fi ` x ) | ( a C_ z /\ A. b e. ( ~P z i^i Fin ) -. X = U. b ) } u.
    { (/) } ) -. u C. v ) $=
      ( vw cv wceq cfn wi wcel wa wss c0 cfi cfv ctg cuni cpw cin wrex wral w3a
      vy vn wn crab csn cun wo wpss ssel elun sseq2 pweq ineq1d raleqdv anbi12d
      wal elrab elsn orbi12i bitri elpwi adantr 0ss sseq1 mpbiri sylbi ralrimiv
      jaoi syl6 unissb sylibr ad2antlr vex uniex elpw uni0b notbii wne disjssun
      biimpcd necon3bd wex n0 elin simprrl simpl ssuni syl2anc exlimiv ad2antrl
      anbi2i syl5bi imp anbi1i unieq syl6eq necon3bi simplrr simprlr finsschain
      uni0 simprr syl22anc expr 0fin mpbir2an eqeq2d notbid rcla4cv mpi syl5bir
      0elpw exp3a com23 ad2antll a1i ss0 biimprcd a1dd syl9r com34 jaod sylan9r
      syl6bi sylan ad2ant2lr adantrl rexlimdv syld sylib ex imp3a cbvralv jca32
      orcom elsnc df-or bitr2i 3bitr4i alrimiv fvex pwex rabex p0ex unex zorn
      syl ) EAMZUAUBZUCUBNFIMZUDNFJMUDNJUUSUEOUFUGPIUUQUEUHGMZUURUEZQUIZFHMZUDZ
      NZULZHUUTUEZOUFZUHZRZUJMZUUTBMZSZUVFHUVLUEZOUFZUHZRZBUVAUMZTUNZUOZSZDMZCM
      ZSUWCUWBSUPCUVKUHDUVKUHZRZUVKUDZUVTQZPZUJVEUWBUWCUQULCUVTUHDUVTUGUVJUWHUJ
      UVJUWEUWGUVJUWERZUWFTNZULZUWFUVAQZUUTUWFSZUVFHUWFUEZOUFZUHZRZRZPZUWGUWIUW
      KUWRUWIUWKRZUWLUWMUWPUWTUWFUURSZUWLUWEUXAUVJUWKUWAUXAUWDUWALMZUURSZLUVKUH
      UXAUWAUXCLUVKUWAUXBUVKQZUXBUVTQZUXCUVKUVTUXBURZUXEUXBUVAQZUUTUXBSZUVFHUXB
      UEZOUFZUHZRZRZUXBTNZUPZUXCUXEUXBUVRQZUXBUVSQZUPUXOUXBUVRUVSUSUXPUXMUXQUXN
      UVQUXLBUXBUVAUVLUXBNZUVMUXHUVPUXKUVLUXBUUTUTUXRUVFHUVOUXJUXRUVNUXIOUVLUXB
      VAVBVCVDVFZLTVGVHVIZUXMUXCUXNUXGUXCUXLUXBUURVJVKUXNUXCTUURSUURVLUXBTUURVM
      VNVQVOVRVPLUVKUURVSVTVKWAUWFUURUVKUJWBWCZWDVTUWIUWKUWMUWKUVKUVSSZULZUWIUW
      MUWJUYBUVKWEWFUWAUYCUWMPUVJUWDUWAUYCUVKUVRUFZTWGZUWMUWAUYBUYDTUYDTNUWAUYB
      UVKUVRUVSWHWIWJUYEUXBUYDQZLWKUWMLUYDWLUYFUWMLUYFUXDUXMRZUWMUYFUXDUXPRUYGU
      XBUVKUVRWMUXPUXMUXDUXSWTVIUYGUXHUXDUWMUXDUXGUXHUXKWNUXDUXMWOUUTUXBUVKWPWQ
      VOWRVOVRWSXAXBUWTFUKMZUDZNZULZUKUWOUHUWPUWTUYKUKUWOUYHUWOQZUYHUWFSZUYHOQZ
      RZUWTUYKUYLUYHUWNQZUYNRUYOUYHUWNOWMUYPUYMUYNUYHUWFUKWBZWDXCVIUWTUYMUYNUYK
      UWTUYNUYMUYKUWIUWKUYNUYMUYKPUWIUWKUYNRZRZUYMUYHUXBSZLUVKUGZUYKUWIUYRUYMVU
      AUWIUYRUYMRZRUVKTWGZUWDUYNUYMVUAUYRVUCUWIUYMUWKVUCUYNUWJUVKTUVKTNUWFTUDZT
      UVKTXDXJXEXFVKWSUVJUWAUWDVUBXGUWIUWKUYNUYMXHUWIUYRUYMXKDCLUVKUYHXIXLXMUYS
      UYTUYKLUVKUWIUYNUXDUYTUYKPZPZUWKUWIUYNVUFUVIUWAUYNVUFPZUVBUWDUVIFVUDNZULZ
      UWAVUGUVITUVHQZVUIVUJTUVGQTOQTUVGOWMUUTYAXNXOUVFVUIHTUVHUVCTNZUVEVUHVUKUV
      DVUDFUVCTXDXPXQXRXSVUIUWARUXDUYNVUEUWAUXDUXEVUIUYNVUEPZUXFUXEUXOVUIVULUXT
      VUIUXMVULUXNUXMVULPVUIUXKVULUXGUXHUXKUYTUYNUYKUYTUYHUXIQZUXKUYNUYKPZUYHUX
      BUYQWDUXKVUMUYNUYKVUMUYNRUYHUXJQUXKUYKUYHUXIOWMUVFUYKHUYHUXJUVCUYHNZUVEUY
      JVUOUVDUYIFUVCUYHXDXPXQXRXTYBXTYCYDYEVUIUXNUYTUYNUYKUXNUYTUYHTNZVUIVUNUXN
      UYTUYHTSVUPUXBTUYHUTUYHYFYMVUIVUPUYKUYNVUPUYKVUIVUPUYJVUHVUPUYIVUDFUYHTXD
      XPXQYGYHYIYJYKXAYLYCYNYOXBYPYQYRXMYCUUAXAVPUYKUVFUKHUWOUYHUVCNZUYJUVEVUQU
      YIUVDFUYHUVCXDXPXQUUBYSUUCYTUWFUVSQZUWFUVRQZUPZVUSVURUPUWSUWGVURVUSUUDVUT
      UWJUWRUPUWSVURUWJVUSUWRUWFTUYAUUEUVQUWQBUWFUVAUVLUWFNZUVMUWMUVPUWPUVLUWFU
      UTUTVVAUVFHUVOUWOVVAUVNUWNOUVLUWFVAVBVCVDVFVHUWJUWRUUFUUGUWFUVRUVSUSUUHYS
      YTUUIDCUJUVTUVRUVSUVQBUVAUURUUQUAUUJUUKUULUUMUUNUUOUUP $.
      $( [27-Jan-2010] $)

    $( Lemma for ~ alexsub .  If a point is covered by a collection taken from
       the base with no finite subcover, a set from the subbase can be added
       that covers the point so that the resulting collection has no finite
       subcover. $)
    alexsublem3 $p |- ( ( ( ( ( J = ( topGen ` ( fi ` x ) ) /\
          A. c e. ~P x ( X = U. c -> E. d e. ( ~P c i^i Fin ) X = U. d ) /\
              a e. ~P ( fi ` x ) ) /\
            ( u e. ~P ( fi ` x ) /\ ( a C_ u /\
              A. b e. ( ~P u i^i Fin ) -. X = U. b ) ) ) /\ w e. u ) /\
          ( ( t e. ( ~P x i^i Fin ) /\ w = |^| t ) /\
            ( y e. w /\ -. y e. U. ( x i^i u ) ) ) ) ->
      E. s e. t A. n e. ( ~P ( u u. { s } ) i^i Fin ) -. X = U. n ) $=
      ( wceq cfn wi wcel wss wa vm vf vv cfi cfv ctg cuni cpw cin wrex wral w3a
      vz cv wn cint csn cun wf wex cdif elin elpwi uncom syl6sseq ssundif sylib
      adantr diffi adantl jca sylbi ad2antll vex anbi1i bitr2i simprrr simplbi2
      elpw2 eldif orcom df-or 3bitr2ri ssriv uniss ax-mp a1i uniun unisn uneq2i
      wo elun eqtri eqsstrd difss sseq2 mpbiri inss1 sseli syl ad3antrrr sseldd
      elssuni unieq 3ad2ant1 syl6eqr sseqtrd unssd eqssd uneq1d rcla4ev syl2anc
      cvv eqeq2d inss2 ex ad2antrl ralrimiv sylibr adantlr ralnex alinexa eleq2
      rcla4v eluni notbid syl9r syl5bi imp31 ad2antrr anim2i eximi elsn orbi12i
      wal eliun bitri syl6 dfrex2 exp32 ad2antlr ctb fibas unitg fiuni 3eqtr4rd
      simprl expr exp3a rexlimdv ralimdva ac6sfi ciun ffvelrn iunss snssd iunfi
      simplrr sylan2 sylan ad2ant2lr snfi sylancl imbi2i albii fveq2 id uneq12d
      unfi unieqd biimpd orbi1i imbi1i bitr4i 3bitri ralbidv imbi12d a4v eleq2d
      eleq1 alrimdv imim1d a1dd syld com14 ralrimdv elint2 syl6ibr sylibrd orrd
      com23 wb orc eqid eqeq1 anbi12d cla4ev mpan2 jaoi anbi2i adantll ad2ant2l
      olc exbii ssrdv hbra1 ax-17 ra4 eqimss2 ssun3 sstr expcom rexlimd syl6req
      syl2im sseq1 syl5ibrcom jaod unissb 3syld ralbii bicomi con4d com24 imp45
      exlimdv 3imtr4g ) GAUNZUDUEZUFUEZOZHLUNZUGOHMUNUGOMUYLUHPUIUJQLUYHUHZUKZJ
      UNZUYIUHZRZULZDUNZUYPRZUYOUYSSZHKUNZUGZOZUOKUYSUHZPUIZUKZTTTCUNZUYSRZEUNZ
      UYMPUIZRZVUHVUJUPZOZTZBUNZVUHRVUPUYHUYSUIUGRUOTZTZHFUNZUGZOZUOFUYSIUNZUQZ
      URZUHZPUIZUKZIVUJUJZUYRUYTVUAVUGVUIVURVVHQQZUYRUYTVUAVUGVVIQUYRUYTVUATZTZ
      VURVUIVUGVVHVVKVURVUIVUGVVHQVVKVURVUITZTZVVHVUGVVMVVAFVVFUJZIVUJUKZVUDKVU
      FUJZVVHUOZVUGUOZVVMVVOHUAUNZUGZVVBURZOZUAVUFUJZIVUJUKZVUJVUFUBUNZUSZHVVBV
      WEUEZUGZVVBURZOZIVUJUKZTZUBUTZVVPVVMVVNVWCIVUJVVMVVBVUJRZTZVVAVWCFVVFVWOV
      USVVFRZVVAVWCVVMVWNVWPVVATZVWCVVMVWNVWQTZTZVUSVVCVAZVUFRZHVWTUGZVVBURZOZV
      WCVWSVWTUYSSZVWTPRZTZVXAVWQVXGVVMVWNVWPVXGVVAVWPVUSVVERZVUSPRZTZVXGVUSVVE
      PVBVXJVXEVXFVXJVUSVVCUYSURZSVXEVXJVUSVVDVXKVXHVUSVVDSVXIVUSVVDVCVHUYSVVCV
      DVEVUSVVCUYSVFVGVXIVXFVXHVUSVVCVIVJVKVLVHVMVXAVWTVUERZVXFTVXGVWTVUEPVBVXL
      VXEVXFVWTUYSDVNZVSVOVPVGVWSHVXCVWSHVUTVXCVVMVWNVWPVVAVQVWSVUTVWTVVCURZUGZ
      VXCVUTVXOSZVWSVUSVXNSVXPAVUSVXNUYHVUSRZUYHVVCRZUOZUYHVWTRZQZUYHVXNRZVXTVX
      QVXSUYHVUSVVCVTVRVYBVXTVXRWKVXRVXTWKVYAUYHVWTVVCWLVXRVXTWAVXRVXTWBWCVGWDV
      USVXNWEWFWGVXOVXBVVCUGZURVXCVWTVVCWHVYCVVBVXBVVBIVNWIWJWMVEWNVWSVXBVVBHVW
      QVXBHSZVVMVWNVVAVYDVWPVVAVYDVXBVUTSZVWTVUSSVYEVUSVVCWOVWTVUSWEWFHVUTVXBWP
      WQVJVMVWSVVBUYHUGZHVWSVVBUYHRVVBVYFSVWSVUJUYHVVBVVLVUJUYHSZVVKVWRVULVYGVU
      NVUQVUIVULVUJUYMRVYGVUKUYMVUJUYMPWRWSVUJUYHVCWTXAUUAVVMVWNVWQUUGXBVVBUYHX
      CWTVWSVYFGUGZHVWSUYJUGZUYIUGZVYHVYFVYIVYJOZVWSUYIUUBRVYKUYHUUCUYIUUDWFZWG
      UYRVYHVYIOZVVJVVLVWRUYKUYNVYMUYQGUYJXDZXEXAVYFVYJOZVWSUYHXMRVYOAVNUYHXMUU
      EWFWGUUFNXFXGXHXIVWBVXDUAVWTVUFVVSVWTOZVWAVXCHVYPVVTVXBVVBVVSVWTXDXJXNXKX
      LUUHUUIUUJUUKVURVWDVWMQZVVKVUIVUOVYQVUQVUOVUJPRZVYQVULVYRVUNVUKPVUJUYMPXO
      WSVHZVYRVWDVWMVWBVWJIUAVUJVUFUBVVSVWGOZVWAVWIHVYTVVTVWHVVBVVSVWGXDXJXNUUL
      XPWTVHXQVVMVWLVVPUBVVMVWLVVPVVMVWLTZIVUJVWGUUMZVUHUQZURZVUFRZHWUDUGZOZVVP
      WUAWUDUYSSZWUDPRZTZWUEWUAWUHWUIWUAWUBWUCUYSVWFWUBUYSSZVVMVWKVWFVWGUYSSZIV
      UJUKWUKVWFWULIVUJVWFVWNWULVWFVWNTZVWGVUFRZWULVUJVUFVVBVWEUUNZWUNVWGVUERZV
      WGPRZTWULVWGVUEPVBWUPWULWUQVWGUYSVCVHVLWTXPXRIVUJVWGUYSUUOXSXQWUAVUHUYSVV
      KVURVUIVWLUURZUUPXHWUAWUBPRZWUCPRWUIVVLVWFWUSVVKVWKVURVWFWUSVUIVUOVWFWUSV
      UQVUOVYRVWFWUSVYSVWFVYRWUQIVUJUKWUSVWFWUQIVUJVWFVWNWUQWUMVUFPVWGVUFPSWUMV
      UEPXOWGWUOXBXPXRIVUJVWGUUQUUSUUTXTXTUVAVUHUVBWUBWUCUVIUVCVKWUEWUDVUERZWUI
      TWUJWUDVUEPVBWUTWUHWUIWUDUYSVXMVSVOVPVGWUAHWUFWUAUCHWUFVVLVWKUCUNZHRZWVAW
      UFRZQZVVKVWFVURVWKWVDVUIVUOVWKWVDVUQVUNVWKWVDVULVUNVWKTZWVBWVAVUPRZVUPVWG
      RZIVUJUJZTZBUTZWVAVUHRZWKZWVCWVEWVBWVLWVEWVBTZWVJWVKWVJUOZWVFWVGUOZIVUJUK
      ZQZBYOZWVMWVKWVRWVFWVHUOZQZBYOWVNWVQWVTBWVPWVSWVFWVGIVUJYAUVDUVEWVFWVHBYB
      VPWVMWVRWVAVUMRZWVKWVMWVRWVAUMUNZRZUMVUJUKWWAWVMWVRWWCUMVUJWVMWWBVUJRZWVR
      WWCVUNVWKWVBWWDWVRWWCQZQWWDVWKWVBVUNWWEWWDVWKHWWBVWEUEZUGZWWBURZOZWVBVUNW
      WEQZQVWJWWIIWWBVUJVVBWWBOZVWIWWHHWWKVWHWWGVVBWWBWWKVWGWWFVVBWWBVWEUVFZUVJ
      WWKUVGUVHXNYDWWIWVBWVAWWHRZWWDWWJWWIWVBWWMHWWHWVAYCUVKWWDWWMWWEVUNWWMWVKV
      UHWWFRZUOZQZCYOZWWCQZWWDWWEWWMWVAWWGRZWWCWKWVKWWNTCUTZWWCWKZWWRWVAWWGWWBW
      LWWSWWTWWCCWVAWWFYEUVLWXAWWTUOZWWCQWWRWWTWWCWBWWQWXBWWCWVKWWNCYBUVMUVNUVO
      WWDWVRWWQWWCWWDWVRWWPCWVRWVKVUHVWGRZUOZIVUJUKZWWDWWOWVQWVKWXEQBCVUPVUHOZW
      VFWVKWVPWXEVUPVUHWVAYCZWXFWVOWXDIVUJWXFWVGWXCVUPVUHVWGUVTYFUVPUVQUVRWXDWW
      OIWWBVUJWWKWXCWWNWWKVWGWWFVUHWWLUVSYFYDYGUWAUWBYHUWCYGUWDUWEYIUWKUWFUMWVA
      VUJUCVNUWGUWHVUNWVKWWAUWLVWKWVBVUHVUMWVAYCYJUWIYHUWJXPWVLWVFWVHWXFWKZTZBU
      TZWVCWVJWXJWVKWVIWXIBWVHWXHWVFWVHWXFUWMYKYLWVKWVFWXFTZBUTZWXJWVKVUHVUHOZW
      XLVUHUWNWXKWVKWXMTBVUHCVNWXFWVFWVKWXFWXMWXGVUPVUHVUHUWOUWPUWQUWRWXKWXIBWX
      FWXHWVFWXFWVHUXCYKYLWTUWSWVCWVFVUPWUDRZTZBUTWXJBWVAWUDYEWXOWXIBWXNWXHWVFW
      XNVUPWUBRZVUPWUCRZWKWXHVUPWUBWUCWLWXPWVHWXQWXFIVUPVUJVWGYPBVUHYMYNYQUWTUX
      DVPVGYRUXAXTXTUXBUXEWUAWVAHSZUCWUDUKWUFHSWUAWXRUCWUDWVAWUDRZWVAVWGRZIVUJU
      JZWVAVUHOZWKZWUAWXRWXSWVAWUBRZWVAWUCRZWKWYCWVAWUBWUCWLWYDWYAWYEWYBIWVAVUJ
      VWGYPUCVUHYMYNYQWUAWYAWXRWYBVWKWYAWXRQVVMVWFVWKWXTWXRIVUJVWJIVUJUXFWXRIUX
      GVWKVWNVWJWXTWXRQVWJIVUJUXHVWJVWIHSZWXTWVAVWISZWXRVWIHUXIWXTWVAVWHSWYGWVA
      VWGXCWVAVWHVVBUXJWTWYGWYFWXRWVAVWIHUXKUXLUXOYRUXMVMWUAWXRWYBVUHHSWUAVUHVY
      JHWUAVUHUYIRVUHVYJSWUAUYSUYIVUHVVKUYSUYISZVVLVWLUYTWYHUYRVUAUYSUYIVCXQYJW
      URXBVUHUYIXCWTUYRVYJHOZVVJVVLVWLUYKUYNWYIUYQUYKVYJVYHHUYKVYHVYIVYJVYNVYLU
      XNNXFXEXAXGWVAVUHHUXPUXQUXRYHXRUCWUDHUXSXSXIVUDWUGKWUDVUFVUBWUDOVUCWUFHVU
      BWUDXDXNXKXLXPUYFUXTVVOVVGUOZIVUJUKVVQVVNWYJIVUJVVAFVVFYSUYAVVGIVUJYAVPVV
      PVVRVUDKVUFYSUYBUYGUYCYTUYDYTUYEYI $.
      $( [14-Dec-2013] $) $( [28-Jan-2010] $)

    $( Lemma for ~ alexsub .  If any cover taken from a subbase has a finite
       subcover, any cover taken from the corresponding base has a finite
       subcover. $)
    alexsublem4 $p |- ( J = ( topGen ` ( fi ` x ) ) ->
      ( A. c e. ~P x ( X = U. c -> E. d e. ( ~P c i^i Fin ) X = U. d ) ->
        A. a e. ~P ( fi ` x ) ( X = U. a ->
          E. b e. ( ~P a i^i Fin ) X = U. b ) ) ) $=
      ( vv cv wceq cuni cfn wrex wi wcel wn wa wss c0 vu vz vy vw vt vn cfi cfv
      vs ctg cpw cin wral w3a ralnex wpss crab csn cun alexsublem2 wo elun pweq
      sseq2 ineq1d raleqdv anbi12d elrab orbi12i bitri notbii ianor df-or orcom
      elsn eqss notnot imbi1i 3bitr4i 3bitri inss2 uniss ax-mp id mpi sylbi wex
      nss df-rex bitr4i sylib eluni2 cint elpwi sseld ad2antrl wb syl6ib adantr
      cvv vex ad2antlr ad2antrr a1i inss1 syl simprl sseldd elpw2 sylibr eqeq2d
      ssun1 unieq notbid ad2antll wne syl5ibrcom adantll adantl elin ex syl5bir
      sseq1 exp3a expr exp32 jctil psseq2 rexlimdv syl5bi dfrex2 rcla4cv anim1i
      mpd con4d elpw anbi1i impr mtod syld mp2an alexsublem3 ssfii sseli syl6ss
      elfi snssd unssd fvex cbvralv biimpi jca32 elun1 snid elun2 intss1 impcom
      adantlr simprlr simprrl elunii sylc con3d com23 imp55 eleq1 elequ1 cla4ev
      adantrrr sylancr eqimss2 necon3bi df-pss rcla4ev syl2anc exp45 mpdd mpbir
      syl5 rexeqdv imbi12d sstr mpan2 3imtr4i reximi2 syl6 cbvrexv a1d 3ad2ant2
      con2d imp3a adantrr biimpd fiuni ctb fibas unitg eqtr4i syl6reqr 3ad2ant1
      syl6eqr syl5sseq eqssd simpll3 simplr ssid sylanbrc rcla4v 0pss nne 0elpw
      0fin mpbir2an psseq1 imbi1d imp jaod 3exp ralrimdv ) BAJZUGUHZUJUHZKZCFJZ
      LZKZCGJZLZKZGUYDUKZMULZNZOZFUXTUKZUMZCDJZLZKZCEJZLZKZEUYPUKZMULZNZOZDUYAU
      KZUYCUYOUYPVUFPZVUEUYCUYOVUGUNZVUDUYRVUDQVUAQZEVUCUMZVUHUYRQZVUAEVUCUOVUH
      VUJVUKVUHVUJRZUAJZIJZUPZQZIUYPUBJZSZVUIEVUQUKZMULZUMZRZUBVUFUQZTURZUSZUMZ
      UAVVENVUKAUBIUABCDEFGHUTVULVVFVUKUAVVEVUMVVEPZVUMVUFPZUYPVUMSZVUIEVUMUKZM
      ULZUMZRZRZVUMTKZVAZVULVVFVUKOZVVGVUMVVCPZVUMVVDPZVAVVPVUMVVCVVDVBVVRVVNVV
      SVVOVVBVVMUBVUMVUFVUQVUMKZVURVVIVVAVVLVUQVUMUYPVDVVTVUIEVUTVVKVVTVUSVVJMV
      UQVUMVCVEVFVGVHUATVOVIVJVULVVNVVQVVOVUHVVNVVQOVUJVUHVVNVVQVUHVVNRZVVFVUML
      ZUXTVUMULZLZKZVUKVWAVWEVVFVWAVWEQZVUOIVVENZVVFQVWFUCJZVWDPZQZUCVWBNZVWAVW
      GVWFVWBVWDSZQZVWKVWFVWDVWBSZVWMOZVWMVWFVWLVWNRZQVWMVWNQZVAZVWOVWEVWPVWBVW
      DVPVKVWLVWNVLVWQVWMVAVWQQZVWMOVWRVWOVWQVWMVMVWMVWQVNVWNVWSVWMVWNVQVRVSVTV
      WOVWNVWMVWCVUMSZVWNUXTVUMWAZVWCVUMWBWCVWOWDWEWFVWMVWHVWBPZVWJRUCWGVWKUCVW
      BVWDWHVWJUCVWBWIWJWKVWAVWJVWGUCVWBVXBVWHUDJZPZUDVUMNVWAVWJVWGOZUDVWHVUMWL
      VWAVXDVXEUDVUMVWAVXCVUMPZVXCUEJZWMZKZUEUYNMULZNZVXDVXEOZVWAVXFVXCUYAPZVXK
      VVHVXFVXMOVUHVVMVVHVUMUYAVXCVUMUYAWNZWOWPVXCWTPUXTWTPZVXMVXKWQUDXAAXAZUEV
      XCUXTWTWTUUFUUAWRVWAVXFVXKVXLOVWAVXFRZVXIVXLUEVXJVXQVXGVXJPZVXIVXLVXQVXRV
      XIRZVXDVWJVWGVXQVXSVXDVWJRZRZRZCUFJZLZKZQZUFVUMUIJZURZUSZUKZMULZUMZUIVXGN
      VWGAUCUDUAUEUFBCUIDEFGHUUBVYBVYLVWGUIVXGVYBVYGVXGPZVYLVWGVYBVYMVYLRZRZVYI
      VVEPZVUMVYIUPZVWGVYOVYIVVCPZVYPVYOVYIVUFPZUYPVYISZVUIEVYKUMZRZRVYRVYOVYSV
      YTWUAVYOVYIUYASVYSVYOVUMVYHUYAVXQVUMUYASZVYAVYNVVNWUCVUHVXFVVHWUCVVMVXNWS
      XBXCVYOVYGUYAVYOUXTUYAVYGUXTUYASZVYOVXOWUDVXPUXTWTUUCWCXDVYOVXGUXTVYGVYAV
      XGUXTSZVXQVYNVXRWUEVXIVXTVXRVXGUYNPWUEVXJUYNVXGUYNMXEUUDVXGUXTWNXFZXCXBVY
      BVYMVYLXGXHXHUUGUUHVYIUYAUXTUGUUIXIXJVYOUYPVUMVYIVXQVVIVYAVYNVVNVVIVUHVXF
      VVHVVIVVLXGXBXCVUMVYHXLZUUEVYLWUAVYBVYMVYLWUAVYFVUIUFEVYKVYCUYSKZVYEVUAWU
      HVYDUYTCVYCUYSXMXKXNUUJUUKXOUULVVBWUBUBVYIVUFVUQVYIKZVURVYTVVAWUAVUQVYIUY
      PVDWUIVUIEVUTVYKWUIVUSVYJMVUQVYIVCVEVFVGVHXJVYIVVCVVDUUMXFVYOVUMVYISZVUMV
      YIXPZRVYQVYOWUKWUJVYOVYIVUMSZQZWUKVYOVUNVYIPZVUNVUMPZQZRZIWGZWUMVYOVYGVYI
      PZVYGVUMPZQZWURVYGVYHPWUSVYGUIXAZUUNVYGVYHVUMUUOWCVXQVXSVXDVWJVYNWVAVXQVX
      SVXDVWJVYNWVAOOVXQVXSVXDRZRVYNVWJWVAVXQWVCVYNVWJWVAOVXQWVCVYNRZRZWUTVWIWV
      EVWHVYGPZVYGUXTPZWUTVWIOWVEVXCVYGVWHVXFWVDVXCVYGSZVWAVXFWVCVYMWVHVYLWVCVY
      MRWVHVXFVXSVYMWVHVXDVXIVYMWVHVXRVYMVXIWVHVYMWVHVXIVXHVYGSVYGVXGUUPVXCVXHV
      YGYCXQUUQXRUURXSUVIXRVXQVXSVXDVYNUUSXHWVEVXGUXTVYGWVCWUEVXQVYNVXRWUEVXIVX
      DWUFXCWPVXQWVCVYMVYLUUTXHWVFWVGWUTVWIWVGWUTRVYGVWCPZWVFVWIVYGUXTVUMXTWVFW
      VIVWIVWHVYGVWCUVAYAYBYDUVBUVCYEUVDYFUVEWUQWUSWVARIVYGWVBVUNVYGKZWUNWUSWUP
      WVAVUNVYGVYIUVFWVJWUOWUTIUIUAUVGXNVGUVHUVJIVYIVUMWHXJWULVUMVYIVYIVUMUVKUV
      LXFWUGYGVUMVYIUVMXJVUOVYQIVYIVVEVUNVYIVUMYHUVNUVOYFYIYNUVPYDYIYAUVQYIYJYI
      UVSVUOIVVEYKWRYOVUHVVNVWEVUKVUHVVNVWERZRZUYRCVWBSZWVLWVMCVWDKZVUHVVNWVNQZ
      VWEVUHVVHVVMWVOVUHVVHRVVIVVLWVOVUHVVIVVLWVOOZOZVVHUYOUYCWVQVUGUYOWVPVVIUY
      OWVNVVLUYOWVNVUAEVVKNZVVLQUYOWVNUYIGVVKNZWVRUYOWVNUYIGVWCUKZMULZNZWVSUYOV
      WCUYNPZWVNWWBOZWWCVWCUXTSZUXTVUMXEZVWCUXTVXPXIUVRUYMWWDFVWCUYNUYDVWCKZUYF
      WVNUYLWWBWWGUYEVWDCUYDVWCXMXKWWGUYIGUYKWWAWWGUYJWVTMUYDVWCVCVEUVTUWAYLWEU
      YIUYIGWWAVVKUYGWWAPZUYGVVKPZUYIUYGVWCSZUYGMPZRZUYGVUMSZWWKRZWWHWWIWWJWWMW
      WKWWJVWTWWMVXAUYGVWCVUMUWBUWCYMWWHUYGWVTPZWWKRWWLUYGWVTMXTWWOWWJWWKUYGVWC
      GXAZYPYQVJWWIUYGVVJPZWWKRWWNUYGVVJMXTWWQWWMWWKUYGVUMWWPYPYQVJUWDYMUWEUWFU
      YIVUAGEVVKUYGUYSKUYHUYTCUYGUYSXMXKUWGWRVUAEVVKYKWRUWJUWHUWIWSUWKYRUWLVUHW
      VKWVMWVNVUHWVKWVMRZRZCVWDVUHWVKWVMCVWDSZVWEWVMWWTOVUHVVNVWEWVMWWTVWBVWDCV
      DUWMXOYRWWSUXTLZVWDCWWEVWDWXASWWFVWCUXTWBWCVUHWXACKZWWRUYCUYOWXBVUGUYCWXA
      BLZCUYCWXCUYBLZWXABUYBXMWXAUYALZWXDVXOWXAWXEKVXPUXTWTUWNWCUYAUWOPWXDWXEKU
      XTUWPUYAUWQWCUWRUWSHUXAUWTWSUXBUXCYEYSWVLWVMUYRUYQVWBSZVVNWXFVUHVWEVVIWXF
      VVHVVLUYPVUMWBWPWPCUYQVWBYCXQYSYEYTYAWSVULVVOVVQVULVVORZVVFVUMUYPUPZQZVUK
      WXGUYPVVEPVVFWXIOWXGVVCVVEUYPVVCVVESWXGVVCVVDXLXDWXGVUGUYPUYPSZVUJRZUYPVV
      CPUYCUYOVUGVUJVVOUXDWXGVUJWXJVUHVUJVVOUXEUYPUXFYGVVBWXKUBUYPVUFVUQUYPKZVU
      RWXJVVAVUJVUQUYPUYPVDWXLVUIEVUTVUCWXLVUSVUBMVUQUYPVCVEVFVGVHUXGXHVUPWXIIU
      YPVVEVUNUYPKVUOWXHVUNUYPVUMYHXNUXHXFVULVVOWXIVUKOZVULWXMVVOTUYPUPZQZVUKOW
      XOUYPTKZVULVUKWXOUYPTXPZQWXPWXNWXQUYPUXIVKUYPTUXJVJVUJWXPVUKOVUHVUJVUKWXP
      CTLZKZQZVUJTVUCPZWXTWYATVUBPTMPTVUBMXTUYPUXKUXLUXMVUIWXTETVUCUYSTKZVUAWXS
      WYBUYTWXRCUYSTXMXKXNYLWEWXPUYRWXSWXPUYQWXRCUYPTXMXKXNXQXSYJVVOWXIWXOVUKVV
      OWXHWXNVUMTUYPUXNXNUXOXQUXPYTYAUXQYJYIYNYAYBYOUXRUXS $.
      $( [14-Dec-2013] $) $( [28-Jan-2010] $)

    $( The Alexander Subbase Theorem: a space is compact iff it has a subbase
       such that any cover taken from the subbase has a finite subcover.
       (Contributed by Jeff Hankins, 24-Jan-2010.) $)
    alexsub $p |- ( J e. Comp <-> E. x ( J = ( topGen ` ( fi ` x ) ) /\ A. c e.
    ~P x ( X = U. c -> E. d e. ( ~P c i^i Fin ) X = U. d ) ) ) $=
      ( vb vy vz vt vw wcel cv wceq cuni cfn wrex wi wa wss va ccmp cfi cfv ctg
      vf cpw cin wral wex alexsublem1 alexsublem4 ctop vex elpw w3a crab eleq2i
      eleq2 3ad2ant3 eluni ssel ctb fibas tg2 3expia mpan syl6bi sylan9r 3impia
      wb sseq2 rcla4ev ex anim2d reximdv syld com23 imp3a exlimdv syl5bi sylbid
      3adant3 elunii expcom syl5rbb biimpd rexlimdv a1d impbid elunirab syl6bbr
      syl9r eqrdv ssrab2 elpw2 mpbir unieq eqeq2d ineq1d rexeqdv imbi12d rcla4v
      fvex pweq ax-mp syl5com elin anbi1i bitri wf sseq1 rexbidv elrab ralrimiv
      simprbi syl6 ac6sfi adantl crn frn adantr ad2antrl wbr simplr wfo wfn ffn
      syl5 dffn4 sylib fodomfi syl2anc domfi jca bitr2i ad2antll syl5bbr eluni2
      cdom id fveq2 sseq12d fnfvelrn sylan sylan2 expr imp44 uniss syl ad2antrr
      a1i4 sseld sylibrd exp32 3exp com34 syl7bi tgcl eleq1 mpbiri jctild iscmp
      ralrimdv syl6ibr imp exlimiv impbii ) BUBLZBAMZUCUDZUEUDZNZCDMZOZNCEMZOZN
      EUVNUGZPUHZQRDUVJUGUIZSZAUJABCDEFUKUWAUVIAUVMUVTUVIUVMUVTCUAMZOZNZCGMZOZN
      ZGUWBUGZPUHZQZRZUAUVKUGZUIZUVIABCUAGDEFULUVMUWMBUMLZBOZUVONZUWOUVQNZEUVSQ
      ZRZDBUGZUIZSUVIUVMUWMUXAUWNUVMUWMUWSDUWTUVNUWTLUVNBTZUVMUWMUWSUVNBDUNZUOU
      VMUXBUWMUWSUVMUXBUWPUWMUWRUVMUXBUWPUWMUWRRUVMUXBUWPUPZUWMUWGGHMZIMZTZIUVN
      QZHUVKUQZUGZPUHZQZUWRUXDCUXIOZNZUWMUXLUXDJCUXMUXDJMZCLZUXOUXELZUXHSZHUVKQ
      ZUXOUXMLUXDUXPUXSUXPUXOUWOLZUXDUXSCUWOUXOFURZUXDUXTUXOUVOLZUXSUWPUVMUXTUY
      BVKUXBUWOUVOUXOUSUTZUVMUXBUYBUXSRUWPUYBUXOKMZLZUYDUVNLZSZKUJUVMUXBSZUXSKU
      XOUVNVAUYHUYGUXSKUYHUYEUYFUXSUYHUYFUYEUXSUVMUXBUYFUYEUXSRUVMUXBUYFUPZUYEU
      XQUXEUYDTZSZHUVKQZUXSUVMUXBUYFUYEUYLRZUXBUYFUYDBLZUVMUYMUVNBUYDVBUVMUYNUY
      DUVLLZUYMBUVLUYDUSUVKVCLZUYOUYMUVJVDZUYPUYOUYEUYLHUYDUVKUXOVEVFVGVHVIVJUY
      IUYKUXRHUVKUYIUYJUXHUXQUYFUVMUYJUXHRUXBUYFUYJUXHUXGUYJIUYDUVNUXFUYDUXEVLV
      MVNUTVOVPVQVFVRVSVTWAWCWBWAUXDUXRUXPHUVKUXDUXRUXPRUXEUVKLUXDUXQUXHUXPUXDU
      XHUXQUXPUXDUXGUXQUXPRZIUVNUXDUXFUVNLZUXGUYRRUXGUXQUXOUXFLZUXDUYSSUXPUXEUX
      FUXOVBUYSUYTUYBUXDUXPUYTUYSUYBUXOUXFUVNWDWEUXDUYBUXPUXPUXTUXDUYBUYAUYCWFW
      GVIWMVNWHVRVSWIWHWJUXHHUXOUVKWKWLWNUXIUWLLZUWMUXNUXLRZRVUAUXIUVKTUXHHUVKW
      OUXIUVKUVJUCXDWPWQUWKVUBUAUXIUWLUWBUXINZUWDUXNUWJUXLVUCUWCUXMCUWBUXIWRWSV
      UCUWGGUWIUXKVUCUWHUXJPUWBUXIXEWTXAXBXCXFXGUXDUWGUWRGUXKUWEUXKLZUWEUXITZUW
      EPLZSZUXDUWGUWRRZVUDUWEUXJLZVUFSVUGUWEUXJPXHVUIVUEVUFUWEUXIGUNUOXIXJUXDVU
      EVUFVUHUXDVUFVUEVUHUXDVUFVUEVUHRUXDVUFSZVUEUWEUVNUFMZXKZUXOUXOVUKUDZTZJUW
      EUIZSZUFUJZVUHVUFVUEVUQRUXDVUEUXOUXFTZIUVNQZJUWEUIZVUFVUQVUEVUSJUWEVUEUXO
      UWELUXOUXILZVUSUWEUXIUXOVBVVAUXOUVKLVUSUXHVUSHUXOUVKUXEUXONUXGVURIUVNUXEU
      XOUXFXLXMXNXPXQXOVUFVUTVUQVURVUNJIUWEUVNUFUXFVUMUXOVLXRVNYIXSVUJVUPVUHUFV
      UJVUPUWGUWRVUJVUPUWGSZSZVUKXTZUVSLZUWOVVDOZNZUWRVVCVVDUVNTZVVDPLZSZVVEVVC
      VVHVVIVUPVVHVUJUWGVULVVHVUOUWEUVNVUKYAYBYCZVVCVUFVVDUWEYTYDZVVIUXDVUFVVBY
      EZVVCVUFUWEVVDVUKYFZVVLVVMVUPVVNVUJUWGVULVVNVUOVULVUKUWEYGZVVNUWEUVNVUKYH
      ZUWEVUKYJYKYBYCUWEVVDVUKYLYMUWEVVDYNYMYOVVEVVDUVRLZVVISVVJVVDUVRPXHVVQVVH
      VVIVVDUVNUXCWPXIYPYKVVCHUWOVVFVVCUXEUWOLZUXEVVFLZVVCVVRUXEUWFLZVVSVVRUXEC
      LZVVCVVTCUWOUXEFURUWGVWAVVTVKVUJVUPCUWFUXEUSYQYRVVTUXEUYDLZKUWEQVVCVVSKUX
      EUWEYSVVCVWBVVSKUWEVUJVULVUOUWGUYDUWELZVWBVVSRZRZVUJVULVUOUWGVWEUXDVULVUO
      VWERZRVUFUXDVULVWFUXDVULSVWCVUOVWDUXDVULVWCVUOVWDRUXDVULVWCSZSZVUOUYDUYDV
      UKUDZTZVWDVWCVUOVWJRUXDVULVUNVWJJUYDUWEUXOUYDNZUXOUYDVUMVWIVWKUUAUXOUYDVU
      KUUBUUCXCYQVWJVWBUXEVWILZVWHVVSUYDVWIUXEVBVWGVWLVVSRUXDVWLVWGVVSVWGVWLVWI
      VVDLZVVSVULVVOVWCVWMVVPUWEUYDVUKUUDUUEUXEVWIVVDWDUUFWEXSWMVQUUGVRVNYBUULU
      UHWHWAWBVVCVVSUXEUVOLZVVRVVCVVFUVOUXEVVCVVHVVFUVOTVVKVVDUVNUUIUUJUUMUXDVV
      RVWNVKZVUFVVBUWPUVMVWOUXBUWOUVOUXEUSUTUUKUUNWJWNUWQVVGEVVDUVSUVPVVDNUVQVV
      FUWOUVPVVDWRWSVMYMUUOVTVQVNVRVSWAWHVQUUPUUQVRUURUVDUVMUWNUVLUMLZUYPVWPUYQ
      UVKUUSXFBUVLUMUUTUVAUVBDEBUVCUVEVQUVFUVGUVH $.
      $( [14-Dec-2013] $) $( [24-Jan-2010] $)
  $}

  ${
    $d o x y J $.  $d o x y X $.
    dfcon2OLD.1 $e |- X = U. J $.
    $( An alternate definition of connectedness.  (Moved into main set.mm as
       ~ dfcon2 and may be deleted by mathbox owner, JGH. --NM 29-May-2014.) $)
    dfcon2OLD $p |- ( J e. Top -> ( J e. Con <-> A. x e. J A. y e. J ( ( x =/=
        (/)
    /\ y =/= (/) /\ ( x i^i y ) = (/) ) -> X =/= ( x u. y ) ) ) ) $=
      ( dfcon2 ) ABCDEF $.
      $( [9-Jul-2009] $)
  $}

  ${
    $d r s u v x y J $.  $d r s u v x y S $.  $d r s u v x y X $.
    connsubOLD.1 $e |- X = U. J $.
    $( Two equivalent ways of saying that a subspace topology is connected.
       (Moved into main set.mm as ~ connsub and may be deleted by mathbox
       owner, JGH. --NM 29-May-2014.) $)
    connsubOLD $p |- ( ( J e. Top /\ S C_ X )
        -> ( ( subSp ` <. S , J >. ) e. Con
    <-> A. x e. J A. y e. J ( ( ( x i^i S ) =/= (/) /\ ( y i^i S ) =/= (/) /\ (
    x i^i y ) C_ ( X \ S ) ) -> -. S C_ ( x u. y ) ) ) ) $=
      ( connsub ) ABCDEFG $.
      $( [15-Dec-2013] $) $( [9-Jul-2009] $)
  $}

  ${
    $d f x y F $.  $d f x y J $.  $d f x y K $.  $d f x y X $.  $d f x y Y $.
    cnconn.1 $e |- X = U. J $.
    cnconn.2 $e |- Y = U. K $.
    $( Connectedness is respected by a continuous onto map.  (Contributed by
       Jeff Hankins, 12-Jul-2009.) $)
    cnconn $p |- ( ( ( J e. Con /\ K e. Top ) /\ ( F : X -onto-> Y /\ F e. ( J
    Cn K ) ) ) -> K e. Con ) $=
      ( vx vy wcel wa c0 wceq wi wb wal cima cvv eleq1 ex ccon ctop wfo co ccld
      ccn cfv cin cuni cpr simplr iscon2 cv wo ccnv cnvexg imaexg anbi12d eqeq1
      orbi12d bibi12d cla4gv 3syl ad2antlr bi1 w3a cnima 3expa adantlrl adantrr
      adantl imp cnclima adantrl pm2.27 eltopss adantll adantlr foimacnv eqcomd
      syldan imaeq2 ima0 syl6eq sylan9eq eqeq2i sylbir adantr ad2antrl ad2antrr
      wss foima 3eqtr3d orim12d syl9r syl5 syld com23 0opn syl5ibrcom 0cld jcad
      mp2and eqid topopn topcld jaod impbid alrimdv elin vex elpr bibi12i albii
      3imtr4g dfcleq exp43 com4l imp5d syl5bi exp31 com4r imp43 sylanbrc ) BUAJ
      ZCUBJZKDEAUCZABCUFUDZJZKZKYFCCUEUGZUHZLCUIZUJZMZCUAJYEYFYJUKYEYFYGYIYOYFY
      GYIYEYOYFYGYIYEYONYEBUBJZBBUEUGZUHZLBUIZUJZMZKYFYGKYIKYOBULYFYGYIYPUUAYOY
      PYFYGYIUUAYONZYPYFYGYIUUBYPYFKZYJKZHUMZYRJZUUEYTJZOZHPZIUMZYLJZUUJYNJZOZI
      PZUUAYOUUDUUEBJZUUEYQJZKZUUELMZUUEYSMZUNZOZHPZUUJCJZUUJYKJZKZUUJLMZUUJYMM
      ZUNZOZIPUUIUUNUUDUVBUVIIUUDUVBUVIUUDUVBKUVEUVHUUDUVBUVEUVHNUUDUVEUVBUVHUU
      DUVEUVBUVHNUUDUVEKZUVBAUOZUUJQZBJZUVLYQJZKZUVLLMZUVLYSMZUNZOZUVHYJUVBUVSN
      ZUUCUVEYIUVTYGYIUVKRJUVLRJUVTAYHUPUVKUUJRUQUVAUVSHUVLRUUEUVLMZUUQUVOUUTUV
      RUWAUUOUVMUUPUVNUUEUVLBSUUEUVLYQSURUWAUURUVPUUSUVQUUEUVLLUSUUEUVLYSUSUTVA
      VBVCVKVDUVSUVOUVRNZUVJUVHUVOUVRVEUVJUVMUVNUWBUVHNUUDUVCUVMUVDUUCYIUVCUVMY
      GUUCYIKZUVCUVMYPYFYIUVCUVMNYPYFYIVFZUVCUVMUUJABCVGTVHVLVIVJUUDUVDUVNUVCUU
      CYIUVDUVNYGUWCUVDUVNYPYFYIUVDUVNNUWDUVDUVNUUJABCVMTVHVLVIVNUVOUWBUVRUVJUV
      HUVOUVRVOUVJUVPUVFUVQUVGUVJUVPUVFUVJUVPUUJAUVLQZLUUDUVCUUJUWEMZUVDUUDUVCU
      UJEWKZUWFUUCUVCUWGYJYFUVCUWGYPUUJCEGVPVQVRZYJUWGUWFUUCYGUWGUWFYIYGUWGKUWE
      UUJDEUUJAVSZVTVRVQWAVJUVPUWEALQLUVLLAWBAWCWDWETUVJUVQUVGUVJUVQKUWEADQZUUJ
      YMUVQUWEUWJMZUVJUVQUVLDMUWKDYSUVLFWFUVLDAWBWGVKUVJUWEUUJMZUVQUUDUVCUWLUVD
      UUDUVCUWGUWLUWHYJUWGUWLUUCYGUWGUWLYIUWIVRVQWAVJWHUUDUWJYMMUVEUVQUUDUWJEYM
      YGUWJEMUUCYIDEAWLWIGWDWJWMTWNWOXCWPWQTWRVLUUDUVHUVENZUVBYFUWMYPYJYFUVFUVE
      UVGYFUVFUVCUVDYFUVCUVFLCJCWSUUJLCSWTYFUVDUVFLYKJCXAUUJLYKSWTXBYFUVGUVCUVD
      YFUVCUVGYMCJCYMYMXDZXEUUJYMCSWTYFUVDUVGYMYKJCYMUWNXFUUJYMYKSWTXBXGVDWHXHT
      XIUUHUVAHUUFUUQUUGUUTUUEBYQXJUUELYSHXKXLXMXNUUMUVIIUUKUVEUULUVHUUJCYKXJUU
      JLYMIXKXLXMXNXOHYRYTXPIYLYNXPXOXQXRXSXTYAYBYCCULYD $.
      $( [12-Jul-2009] $)
  $}

  ${
    $d f J $.  $d f K $.
    $( Connectedness is a topological property.  (Contributed by Jeff Hankins,
       3-Jul-2009.) $)
    conntoppr $p |- ( ( J e. Top /\ K e. Top /\ J ~= K ) -> ( J e. Con <-> K e.
    Con ) ) $=
      ( vf ctop wcel chomeo ccon wa cuni wfo ccn wf1o 3impia f1ofo jca32 cnconn
      co eqid expl syl5com wbr wb cv chomeosm wex c0 wne hmph n0 syl6bb hmeomap
      w3a simp2 syl ccnv hmeobc biimp3a simpld f1ocnv 3syl simprd impbid 3expia
      simp1 exlimdv sylbid ) ADEZBDEZABFUAZAGEZBGEZUBZVGVHHZVICUCZABUDQZEZCUEZV
      LVMVIVOUFUGVQABUHCVOUIUJVMVPVLCVGVHVPVLVGVHVPULZVJVKVRVHAIZBIZVNJZVNABKQE
      ZHZHVJVKVRVHWAWBVGVHVPUMVRVSVTVNLZWAVGVHVPWDVNABVSVTVSRZVTRZUKMZVSVTVNNUN
      VRWBVNUOZBAKQEZVGVHVPWBWIHVNABUPUQZUROVJVHWCVKVNABVSVTWEWFPSTVRVGVTVSWHJZ
      WIHZHVKVJVRVGWKWIVGVHVPVDVRWDVTVSWHLWKWGVSVTVNUSVTVSWHNUTVRWBWIWJVAOVKVGW
      LVJWHBAVTVSWFWEPSTVBVCVEVFM $.
      $( [3-Jul-2009] $)
  $}

