$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Directed sets, nets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d d x D $.  $d x X $.  $d x A $.
    tailfval.1 $e |- X = dom D $.
    $( The tail function for a directed set.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tailfval $p |- ( D e. DirRel -> ( tail ` D ) =
                     ( x e. X |-> ( D " { x } ) ) ) $=
      ( cdir wcel ctail cfv cuni csn cima cmpt cvv wceq uniexg mptexg mpteq12dv
      vd cv 3syl unieq unieqd imaeq1 df-tail fvmptg mpdan cdm dirdm eqidd eqtrd
      syl5req ) BEFZBGHZABIZIZBASJZKZLZACUQLULURMFZUMURNULUNMFUOMFUSBEOUNMOAUOU
      QMPTRBARSZIZIZUTUPKZLUREMGUTBNZAVBVCUOUQVDVAUNUTBUAUBUTBUPUCQARUDUEUFULAU
      OUQCUQULCBUGUODBUHUKULUQUIQUJ $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)

    $( The tail of an element in a directed set.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tailval $p |- ( ( D e. DirRel /\ A e. X ) ->
                   ( ( tail ` D ) ` A ) = ( D " { A } ) ) $=
      ( vx cdir wcel wa ctail cfv csn cima cmpt wceq tailfval fveq1d adantr cvv
      cv id imaexg sneq imaeq2d eqid fvmptg syl2anr eqtrd ) BFGZACGZHABIJZJZAEC
      BESZKZLZMZJZBAKZLZUHUKUPNUIUHAUJUOEBCDOPQUIUIURRGUPURNUHUITBUQFUAEAUNURCR
      UOULANUMUQBULAUBUCUOUDUEUFUG $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)

    $( An element of a tail.  (Contributed by Jeff Hankins, 25-Nov-2009.) $)
    eltail $p |- ( ( D e. DirRel /\ A e. X /\ B e. C ) ->
                   ( B e. ( ( tail ` D ) ` A ) <-> A D B ) ) $=
      ( cdir wcel w3a ctail cfv csn cima wbr wb wa tailval eleq2d 3adant3 cop
      elimasng df-br syl6bbr 3adant1 bitrd ) DGHZAEHZBCHZIBADJKKZHZBDALMZHZABDN
      ZUFUGUJULOUHUFUGPUIUKBADEFQRSUGUHULUMOUFUGUHPULABTDHUMDABECUAABDUBUCUDUE
      $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    $d s x y z D $.  $d s x y z X $.
    tailf.1 $e |- X = dom D $.
    $( The tail function of a directed set sends its elements to its subsets.
       (Contributed by Jeff Hankins, 25-Nov-2009.) $)
    tailf $p |- ( D e. DirRel -> ( tail ` D ) : X --> ~P X ) $=
      ( vx cdir wcel cpw ctail cfv wf cv csn cima cmpt wral wss cuni cvv mpbird
      sstri crn imassrn cdm cun ssun2 dmrnssfld dirdm syl5req syl5sseq wb dmexg
      syl5eqel elpw2g syl ralrimivw eqid fmpt sylib tailfval feq1d ) AEFZBBGZAH
      IZJBVBDBADKLZMZNZJZVAVEVBFZDBOVGVAVHDBVAVHVEBPZVAAQQZVEBVEAUAZVJAVDUBVKAU
      CZVKUDVJVKVLUEAUFTTVABVLVJCAUGUHUIVABRFVHVIUJVABVLRCAEUKULVEBRUMUNSUODBVB
      VEVFVFUPUQURVABVBVCVFDABCUSUTS $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    tailini.1 $e |- X = dom D $.
    $( A tail contains its initial element.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tailini $p |- ( ( D e. DirRel /\ A e. X ) -> A e. ( ( tail ` D ) ` A ) ) $=
      ( cdir wcel wa ctail cfv wbr dirref wb eltail 3anidm23 mpbird ) BEFZACFZG
      AABHIIFZAABJZABCDKPQRSLAACBCDMNO $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d t x D $.  $d t x X $.
    tailuni.1 $e |- X = dom D $.
    $( The union of all the tails of a directed set.  (Contributed by Jeff
       Hankins, 13-Dec-2009.) $)
    tailuni $p |- ( D e. DirRel -> U. ran ( tail ` D ) = X ) $=
      ( vx vt cdir wcel ctail cfv crn cuni cv wss wral cpw wf wi syl sylibr wa
      tailf frn sseld vex elpw syl6ib ralrimiv unissb wex tailini wfn ffn sylan
      fnfvelrn fvex wceq eleq2 eleq1 anbi12d cla4ev syl2anc eluni ssrdv eqssd
      ex ) AFGZAHIZJZKZBVFDLZBMZDVHNVIBMVFVKDVHVFBBOZVGPZVJVHGZVKQABCUAZVMVNVJV
      LGVKVMVHVLVJBVLVGUBUCVJBDUDUEUFRUGDVHBUHSVFDBVIVFVJBGZVJVIGZVFVPTZVJELZGZ
      VSVHGZTZEUIZVQVRVJVJVGIZGZWDVHGZWCVJABCUJVFVGBUKZVPWFVFVMWGVOBVLVGULRBVJV
      GUNUMWBWEWFTEWDVJVGUOVSWDUPVTWEWAWFVSWDVJUQVSWDVHURUSUTVAEVJVHVBSVEVCVD
      $.
      $( [13-Dec-2009] $)
  $}

  ${
    $d u v w x y z D $.  $d u v w x y z X $.
    tailfb.1 $e |- X = dom D $.
    $( The collection of tails of a directed set is a filter base.
       (Contributed by Jeff Hankins, 25-Nov-2009.) $)
    tailfb $p |- ( ( D e. DirRel /\ X =/= (/) )
          -> ran ( tail ` D ) e. fBas ) $=
      ( vz vx vy vu vv vw wcel c0 wa cfv cv wss wrex wi wb wbr cvv cdir wne crn
      ctail wnel cin wral cfbas wex n0 cpw wf wfn tailf fnfvelrn 3syl ne0i syl6
      ffn ex exlimdv syl5bi imp wn tailini n0i syl nrexdv adantr fvelrnb mtbird
      df-nel sylibr anbi12d reeanv dirge 3expb sylan ad2ant2r dirtr exp32 mpan2
      wceq vex com23 ad2ant2rl anim12d expr impr eltail adantrr adantrl 3imtr4d
      mp3an3 elin syl6ibr ssrdv sseq1 rcla4ev syl2anc rexlimdv mpd ineq1 sseq2d
      rexbidv ineq2 sylan9bb syl5ibcom rexlimdvva syl5bir sylbid ralrimivv fvex
      w3a rnex isfbas2 ax-mp syl3anbrc ) AUAJZBKUBZLZAUDMZUCZKUBZKYCUEZDNZENZFN
      ZUFZOZDYCPZFYCUGEYCUGZYCUHJZXSXTYDXTYGBJZEUIXSYDEBUJXSYNYDEXSYNYGYBMZYCJZ
      YDXSBBUKZYBULZYBBUMZYNYPQABCUNZBYQYBUSZYSYNYPBYGYBUOUTUPYCYOUQURVAVBVCYAK
      YCJZVDYEYAUUBYOKWCZEBPZXSUUDVDXTXSUUCEBXSYNLYGYOJUUCVDYGABCVEYOYGVFVGVHVI
      XSUUBUUDRZXTXSYRYSUUEYTUUAEBKYBVJUPVIVKKYCVLVMYAYKEFYCYCXSYGYCJZYHYCJZLZY
      KQXTXSUUHGNZYBMZYGWCZGBPZHNZYBMZYHWCZHBPZLZYKXSYRYSUUHUUQRYTUUAYSUUFUULUU
      GUUPGBYGYBVJHBYHYBVJVNUPUUQUUKUUOLZHBPGBPXSYKUUKUUOGHBBVOXSUURYKGHBBXSUUI
      BJZUUMBJZLZLZYFUUJUUNUFZOZDYCPZUURYKUVBUUIINZASZUUMUVFASZLZIBPZUVEXSUUSUU
      TUVJIUUIUUMABCVPVQUVBUVIUVEIBUVBUVFBJZUVIUVEUVBUVKUVILZLZUVFYBMZYCJZUVNUV
      COZUVEXSUVKUVOUVAUVIXSYSUVKUVOXSYRYSYTUUAVGBUVFYBUOVRVSUVMEUVNUVCUVMYGUVN
      JZYGUUJJZYGUUNJZLZYGUVCJUVMUVFYGASZUUIYGASZUUMYGASZLZUVQUVTUVBUVKUVIUWAUW
      DQUVBUVKLUWAUVIUWDUVBUVKUWAUVIUWDQUVBUVKUWALLUVGUWBUVHUWCXSUWAUVGUWBQZUVA
      UVKXSUWAUWEXSUVGUWAUWBXSYGTJZUVGUWAUWBQQEWDZXSUWFLZUVGUWAUWBUUIUVFYGATVTW
      AWBWEVCWFXSUWAUVHUWCQZUVAUVKXSUWAUWIXSUVHUWAUWCXSUWFUVHUWAUWCQQUWGUWHUVHU
      WAUWCUUMUVFYGATVTWAWBWEVCWFWGWHWEWIXSUVKUVQUWARZUVAUVIXSUVKUWFUWJUWGUVFYG
      TABCWJWNVSUVBUVTUWDRUVLUVBUVRUWBUVSUWCXSUUSUVRUWBRZUUTXSUUSUWFUWKUWGUUIYG
      TABCWJWNWKXSUUTUVSUWCRZUUSXSUUTUWFUWLUWGUUMYGTABCWJWNWLVNVIWMYGUUJUUNWOWP
      WQUVDUVPDUVNYCYFUVNUVCWRWSWTWAXAXBUUKUVEYFYGUUNUFZOZDYCPUUOYKUUKUVDUWNDYC
      UUKUVCUWMYFUUJYGUUNXCXDXEUUOUWNYJDYCUUOUWMYIYFUUNYHYGXFXDXEXGXHXIXJXKVIXL
      YCTJYMYDYEYLXNRYBAUDXMXOEFDTYCXPXQXR $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d d f m n t v x y w z F $.  $d d f m n t w z X $.
    filnet.1 $e |- X = U. F $.
    ${
      $d t v w x y z H $.  $d t v w z D $.
      filnet.2 $e |- H = { <. m , n >. | ( n e. F /\ m e. n ) } $.
      filnet.3 $e |- D = { <. x , y >. |
        ( ( x e. H /\ y e. H ) /\ ( 2nd ` y ) C_ ( 2nd ` x ) ) } $.
      ${
        $d A x y $.  $d B x y $.
        filnetlem1.4 $e |- A e. _V $.
        filnetlem1.5 $e |- B e. _V $.
        $( Lemma for ~ filnet .  Change variables. $)
        filnetlem1 $p |- ( A D B <-> ( ( A e. H /\ B e. H ) /\
          ( 2nd ` B ) C_ ( 2nd ` A ) ) ) $=
          ( wcel wa c2nd cfv wss wceq eleq1 anbi1d sseq2d anbi12d anbi2d sseq1d
          cv fveq2 brab ) AUHZIPZBUHZIPZQZUMRSZUKRSZTZQCIPZUNQZUPCRSZTZQUSDIPZQ
          ZDRSZVATZQABCDENOUKCUAZUOUTURVBVGULUSUNUKCIUBUCVGUQVAUPUKCRUIUDUEUMDU
          AZUTVDVBVFVHUNVCUSUMDIUBUFVHUPVEVAUMDRUIUGUEMUJ $.
          $( [23-Nov-2013] $) $( [13-Dec-2009] $)
      $}

      $( Lemma for ~ filnet .  The field of the direction. $)
      filnetlem2 $p |- ( H = U. U. D /\ H = dom D ) $=
        ( vz cuni cv wcel wa c2nd cfv wss sstri wceq cdm crn cun wbr filnetlem1
        ssid mpbiran2 breldm sylbir anidms ssriv ssun1 dmrnssfld copab opabssxp
        vex cxp eqsstri uniss mp2b unixpss unidm eqssi dmss ax-mp dmxpid pm3.2i
        sseqtri ) GCMZMZUAGCUBZUAGVKGVLCUCZUDZVKGVLVNLGVLLNZGOZVOVLOZVPVPPZVOVO
        CUEZVQVSVRVOQRZVTSABVOVOCDEFGHIJKLUQZWAUFVTUGUHVOVOCWAUIUJUKULZVLVMUMTC
        UNTVKGGUDZGVKGGURZMZMZWCCWDSZVJWESVKWFSCANZGOBNZGOPWIQRWHQRSZPABUOWDKWJ
        ABGGUPUSZCWDUTVJWEUTVAGGVBTGVCVIVDGVLWBVLWDUBZGWGVLWLSWKCWDVEVFGVGVIVDV
        H $.
        $( [23-Nov-2013] $) $( [13-Dec-2009] $)

      $( Lemma for ~ filnet . $)
      filnetlem3 $p |- ( H C_ ( X X. F ) /\ ( F e. Fil -> D e. DirRel ) ) $=
        ( vv vw vz wss wcel wa wbr c2nd cvv vt cxp cfil cdir wi cv copab elunii
        cuni syl6eleqr simpl jca ssopab2i df-xp 3sstr4i wrel cid cres ccom ccnv
        ancoms wex wral cfv cin c0 c1st wceq eleq1 anbi2d eleq2 anbi12d elopabi
        wne eleq2s simpld fveq2 eleq1d vtoclga anim12i fipfil imp n0 w3a simp2l
        syl5 cop filint 3expb sylan2 3adant3 simp3 df-br fvex inex1 brab bitr3i
        sylanbrc op2nd inss1 syl5eqss opex filnetlem1 simp2r inss2 breq2 cla4ev
        vex a1i syl2anc 3expia exlimdv syl5bi mpd ralrimivva sylibr wal simplll
        codir simprlr sstr ad2ant2l jca31 anbi12i 3imtr4i gen2 cotr mpbir jctil
        ax-gen relopabi ssid mpbiran2 biimpri anidms pm3.2i xpexg ssexg sylancr
        rgen ref4w mpbi opabssxp eqsstri uniexg syl5eqel mpancom cdm filnetlem2
        wb simpli isdir syl mpbird ) GHFUBZOZFUCPZCUDPZUEEUFZFPZDUFZUUSPZQZDEUG
        ZUVAHPZUUTQZDEUGGUUOUVCUVFDEUVCUVEUUTUVCUVAFUIZHUVBUUTUVAUVGPUVAUUSFUHV
        AIUJUUTUVBUKULUMJDEHFUNUOZUUQUURCUPZUQGURCOZQZCCUSCOZGGUBZCUTCUSOZQZQZU
        UQUVOUVKUUQUVNUVLUUQUAUFZLUFZCRZMUFZUVRCRZQZLVBZMGVCUAGVCUVNUUQUWCUAMGG
        UUQUVQGPZUVTGPZQZQZUVQSVDZUVTSVDZVEZVFVNZUWCUUQUWFUWKUWFUWHFPZUWIFPZQZU
        UQUWKUWDUWLUWEUWMUWDUWLUVQVGVDZUWHPZUWLUWPQZUVQUVDGUVCUUTUWOUUSPZQUWQDE
        UVQUVAUWOVHUVBUWRUUTUVAUWOUUSVIVJUUSUWHVHUUTUWLUWRUWPUUSUWHFVIUUSUWHUWO
        VKVLVMJVOVPZUWLUWMUAUVTGUVQUVTVHUWHUWIFUVQUVTSVQVRUWSVSVTZUWHUWIFWAWFWB
        UWKNUFZUWJPZNVBUWGUWCNUWJWCUWGUXBUWCNUUQUWFUXBUWCUUQUWFUXBWDZUVQUXAUWJW
        GZCRZUVTUXDCRZUWCUXCUWDUXDGPZQUXDSVDZUWHOUXEUXCUWDUXGUUQUWDUWEUXBWEUXCU
        WJFPZUXBUXGUUQUWFUXIUXBUWFUUQUWNUXIUWTUUQUWLUWMUXIUWHUWIFWHWIWJWKUUQUWF
        UXBWLUXGUXAUWJGRUXIUXBQZUXAUWJGWMUVCUUTUXAUUSPZQUXJDEUXAUWJGNXHZUWHUWIU
        VQSWNWOZUVAUXAVHUVBUXKUUTUVAUXAUUSVIVJUUSUWJVHUUTUXIUXKUXBUUSUWJFVIUUSU
        WJUXAVKVLJWPWQWRZULUXCUXHUWJUWHUXAUWJUXLUXMWSZUWJUWHOUXCUWHUWIWTXIXAABU
        VQUXDCDEFGHIJKUAXHUXAUWJXBZXCWRUXCUWEUXGQUXHUWIOUXFUXCUWEUXGUUQUWDUWEUX
        BXDUXNULUXCUXHUWJUWIUXOUWJUWIOUXCUWHUWIXEXIXAABUVTUXDCDEFGHIJKMXHZUXPXC
        WRUWBUXEUXFQLUXDUXPUVRUXDVHUVSUXEUWAUXFUVRUXDUVQCXFUVRUXDUVTCXFVLXGXJXK
        XLXMXNXOUAMLGGCXSXPUVLUVRUVTCRZUVTUXACRZQZUVRUXACRZUEZNXQZMXQLXQUYCLMUY
        BNUVRGPZUWEQZUWIUVRSVDZOZQZUWEUXAGPZQZUXASVDZUWIOZQZQZUYDUYIQUYKUYFOZQU
        XTUYAUYNUYDUYIUYOUYDUWEUYGUYMXRUYHUWEUYIUYLXTUYGUYLUYOUYEUYJUYLUYGUYOUY
        KUWIUYFYAVAYBYCUXRUYHUXSUYMABUVRUVTCDEFGHIJKLXHZUXQXCABUVTUXACDEFGHIJKU
        XQUXLXCYDABUVRUXACDEFGHIJKUYPUXLXCYEYJYFLMNCYGYHYIUVIUVJAUFZGPBUFZGPQUY
        RSVDUYQSVDOZQZABCKYKUXAUXACRZNGVCUVJVUANGUYIVUAVUAUYIUYIQZVUAVUBUYKUYKO
        ABUXAUXACDEFGHIJKUXLUXLXCUYKYLYMYNYOYTNGCUUAUUBYPYIUUQCTPZUURUVPUUJUUQC
        UVMOUVMTPZVUCCUYTABUGUVMKUYSABGGUUCUUDUUQGTPZVUEVUDUUQUUPUUOTPZVUEUVHHT
        PUUQVUFUUQHUVGTIFUCUUEUUFHFTUCYQUUGGUUOTYRYSZVUGGGTTYQXJCUVMTYRYSGCTGCU
        IUIVHGCUUHVHABCDEFGHIJKUUIUUKUULUUMUUNYP $.
        $( [24-Nov-2013] $) $( [13-Dec-2009] $)

      $( Lemma for ~ filnet . $)
      filnetlem4 $p |- ( F e. Fil -> E. z e. DirRel E. w ( w : dom z --> X
       /\ F = ( ( X FilMap ran ( tail ` z ) ) ` w ) ) ) $=
        ( wcel cfv wceq wa wss c1st cvv vt vv cfil cdir cv wf ctail crn cfm wex
        co cdm wrex cxp wi filnetlem3 simpri cres wfun wfo fo1st fof ax-mp ffun
        simpli cuni syl5eqel xpexg mpancom ssexg sylancr resfunexg cima elssuni
        uniexg syl6sseqr a1i c0 wne filesn eleq1 notbid syl5ibrcom necon2ad imp
        wn n0 sylib w3a cop wfn cpw filnetlem2 ffn 3syl 3ad2ant1 3simpc wbr vex
        tailf anbi2d eleq2 anbi12d brab df-br bitr3i fnfvelrn syl2anc wb eltail
        wral mp3an3 opex brelrn sseli syl syl6bi filnetlem1 op2nd fveq2 eqeltrd
        c2nd ssrdv funimass4 mpbird imaeq2 sseq1d rcla4ev 3expia exlimdv mpd ex
        adantr eleq1d sylan impancom sylbid rexlimdv feq2d eqeq2d cun dmrnssfld
        ssun2 sstri sseqtr4i fvres biimpa simprbi syl6sseq eleq12d copab eleq2s
        elopabi simprd vtoclga sseldd ralrimiva fssres mp2an fdmi jcad biimprcd
        fvelrnb reximdv syl6 ralbidva bitrd simpll simpld ad2antlr simprl simpl
        ssv anim1i fvex eqimssi mpbiran2 sylanbrc adantll syl6eq rcla4v adantrl
        op1st fillsb syl13anc exp3a syl9r com23 impbid cfbas emnfil ne0i tailfb
        expimpd ffnfv xp1st rgen mpbir2an tailuni mpbiri eqid elfm bitr4d eqrdv
        syl3anc jctil feq1 cla4egv sylc dmeq syl6eqr rneqd oveq2d fveq1d exbidv
        ) HUCNZEUDNZIJDUEZUFZHUXRJEUGOZUHZUIUKZOZPZQZDUJZCUEZULZJUXRUFZHUXRJUYG
        UGOZUHZUIUKZOZPZQZDUJZCUDUMIJHUNZRZUXPUXQUOZABEFGHIJKLMUPZUQZUXPSIURZTN
        ZIJVUBUFZHVUBUYBOZPZQZUYFUXPSUSZITNZVUCTTSUFZVUHTTSUTVUJVATTSVBVCZTTSVD
        VCUXPUYRUYQTNZVUIUYRUYSUYTVEZJTNZUXPVULUXPJHVFZTKHUCVOVGZJHTUCVHVIIUYQT
        VJVKSITVLVKUXPVUFVUDUXPUAHVUEUXPUAUEZHNZVUQJRZVUBUYGVMZVUQRZCUYAUMZQZVU
        QVUENZUXPVURVVCUXPVURVUSVVBVURVUSUOUXPVURVUQVUOJVUQHVNKVPVQUXPVURVVBUXP
        VURQZUBUEZVUQNZUBUJZVVBVVEVUQVRVSZVVHUXPVURVVIUXPVURVUQVRUXPVURWFVUQVRP
        ZVRHNZWFHVTVVJVURVVKVUQVRHWAWBWCWDWEUBVUQWGWHZVVEVVGVVBUBUXPVURVVGVVBUX
        PVURVVGWIZVVFVUQWJZUXTOZUYANZVUBVVOVMZVUQRZVVBVVMUXTIWKZVVNINZVVPUXPVUR
        VVSVVGUXPUXQIIWLZUXTUFZVVSVUAEIIEVFVFZPZIEULZPZABEFGHIJKLMWMZUQZWTZIVWA
        UXTWNZWOWPVVMVURVVGQZVVTUXPVURVVGWQVWKVVFVUQIWRVVTGUEZHNZFUEZVWLNZQZVWM
        VVFVWLNZQZVWKFGVVFVUQIUBWSZUAWSZVWNVVFPVWOVWQVWMVWNVVFVWLWAXAZVWLVUQPVW
        MVURVWQVVGVWLVUQHWAVWLVUQVVFXBXCLXDVVFVUQIXEXFWHZIVVNUXTXGXHVVMVVRUYGVU
        BOZVUQNZCVVOXKZVVMVXDCVVOVVMUYGVVONZQZVXCUYGSOZVUQVXGUYGINZVXCVXHPZVVMV
        XFVXIVVMVXFVVNUYGEWRZVXIVVMUXQVVTVXFVXKXIZUXPVURUXQVVGVUAWPVXBUXQVVTUYG
        TNZVXLCWSZVVNUYGTEIVWHXJXLXHZVXKUYGEUHZNZVXIVVNUYGEVVFVUQXMZVXNXNVXPIUY
        GVXPVWCIVXPVWEVXPUUAVWCVXPVWEUUCEUUBUUDVWDVWFVWGVEUUEXOZXPXQZWEZUYGISUU
        FZXPVXGUYGYBOZVUQVXHVXGVXKVYCVUQRVVMVXFVXKVXOUUGVXKVYCVVNYBOZVUQVXKVVTV
        XIQVYCVYDRABVVNUYGEFGHIJKLMVXRVXNXRUUHVVFVUQVWSVWTXSUUIXPVXGVXIVXHVYCNZ
        VYAUXRSOZUXRYBOZNZVYEDUYGIUXRUYGPVYFVXHVYGVYCUXRUYGSXTUXRUYGYBXTUUJUXRI
        NZVYGHNZVYHVYJVYHQZUXRVWPFGUUKIVWPVWMVYFVWLNZQVYKFGUXRVWNVYFPVWOVYLVWMV
        WNVYFVWLWAXAVWLVYGPZVWMVYJVYLVYHVWLVYGHWAZVWLVYGVYFXBXCUUMLUULZUUNUUOXP
        UUPYAUUQVVMVUBUSZVVOVUBULZRVVRVXEXIITVUBUFZVYPVUJITRVYRVUKIUVMTTISUURUU
        SZITVUBVDVCZVVMVVOIVYQVVMCVVOIVXTYCITVUBVYSUUTZVPCVVOVUQVUBYDVKYEVVAVVR
        CVVOUYAUYGVVOPVUTVVQVUQUYGVVOVUBYFYGYHXHYIYJYKYLUVAUXPVUSVVBVURUXPVUSQZ
        VVAVURCUYAWUBUYGUYANZUXRUXTOZUYGPZDIUMZVVAVURUOWUBUXQWUCWUFXIZUXPUXQVUS
        VUAYMUXQVWBVVSWUGVWIVWJDIUYGUXTUVCWOXPWUBVVAWUFVURVVAWUFVUBWUDVMZVUQRZD
        IUMWUBVURVVAWUEWUIDIWUEWUIVVAWUEWUHVUTVUQWUDUYGVUBYFYGUVBUVDWUBWUIVURDI
        UXPVYIVUSWUIVURUOUXPVYIQZVUSWUIVURWUJVUSWUIQVUSVXHVUQNZCWUDXKZQZVURWUJW
        UIWULVUSUXPUXQVYIWUIWULXIVUAUXQVYIQZWUIVXDCWUDXKZWULWUNVYPWUDVYQRWUIWUO
        XIVYTWUNWUDIVYQWUNCWUDIWUNUYGWUDNZVXQVXIWUNWUPUXRUYGEWRZVXQUXQVYIVXMWUP
        WUQXIVXNUXRUYGTEIVWHXJXLUXRUYGEDWSZVXNXNXQVXSUVEZYCWUAVPCWUDVUQVUBYDVKW
        UNVXDWUKCWUDWUNWUPQZVXCVXHVUQWUTVXIVXJWUNWUPVXIWUSWEVYBXPYNUVFUVGYOXAWU
        JWUMVURWUJWUMQUXPVYJVUSVYGVUQRZVURUXPVYIWUMUVHVYIVYJUXPWUMVYIVYJVYHVYOU
        VIZUVJWUJVUSWULUVKWUJWULWVAVUSWUJWULQUBVYGVUQWUJVVFVYGNZWULVVGWUJWVCQZV
        VFVYGWJZWUDNZWULVVGUOWVDWVFUXRWVEEWRZVYIWVCWVGUXPVYIWVCQZVYIWVEINZWVGVY
        IWVCUVLWVHVYJWVCQZWVIVYIVYJWVCWVBUVNWVJVVFVYGIWRWVIVWPVWRWVJFGVVFVYGIVW
        SUXRYBUVOZVXAVYMVWMVYJVWQWVCVYNVWLVYGVVFXBXCLXDVVFVYGIXEXFWHWVGVYIWVIQW
        VEYBOZVYGRABUXRWVEEFGHIJKLMWURVVFVYGXMZXRWVLVYGVVFVYGVWSWVKXSUVPUVQUVRU
        VSWUJWVFWVGXIZWVCUXPUXQVYIWVNVUAUXQVYIWVETNWVNWVMUXRWVETEIVWHXJXLYOYMYE
        WUKVVGCWVEWUDUYGWVEPZVXHVVFVUQWVOVXHWVESOVVFUYGWVESXTVVFVYGVWSUWCUVTYNU
        WAXPYPYCUWBUXPVYJVUSWVAWIVURVYGVUQHJKUWDWEUWEYLYQUWFYPYRUWGUWHYQYRUWNUW
        IUXPVUNUYAUWJNZUYAVFZJVUBUFZVVDVVCXIVUPUXPUXQIVRVSZWVPVUAUXPVURUAUJZWVS
        UXPHVRVSWVTHUWKUAHWGWHUXPVURWVSUAUXPVURWVSVVEVVHWVSVVLVVEVVGWVSUBUXPVUR
        VVGWVSVVMVVTWVSVXBIVVNUWLXPYIYJYKYLYJYKEIVWHUWMXHUXPWVRVUDVUDVUBIWKZVXC
        JNZCIXKCIJVUBUWOVYRWWAVYSITVUBWNVCWWBCIVXIVXCVXHJVYBVXIUYGUYQNVXHJNIUYQ
        UYGVUMXOUYGJHUWPXPYAUWQUWRZUXPWVQIJVUBUXPUXQWVQIPVUAEIVWHUWSXPYSUWTCVUQ
        UYATVUBJWVQWVQUXAUXBUXEUXCUXDWWCUXFUYEVUGDVUBTUXRVUBPZUXSVUDUYDVUFIJUXR
        VUBUXGWWDUYCVUEHUXRVUBUYBXTYTXCUXHUXIUYPUYFCEUDUYGEPZUYOUYEDWWEUYIUXSUY
        NUYDWWEUYHIJUXRWWEUYHVWEIUYGEUXJVWHUXKYSWWEUYMUYCHWWEUXRUYLUYBWWEUYKUYA
        JUIWWEUYJUXTUYGEUGXTUXLUXMUXNYTXCUXOYHXH $.
        $( [14-Dec-2013] $) $( [15-Dec-2009] $)
    $}

    $( A filter has the same convergence and clustering properties as some
       net.  (Contributed by Jeff Hankins, 12-Dec-2009.) $)
    filnet $p |- ( F e. Fil -> E. d e. DirRel E. f ( f : dom d --> X
       /\ F = ( ( X FilMap ran ( tail ` d ) ) ` f ) ) ) $=
      ( vx vy vn vm cv wcel wa copab c2nd cfv wss eqid filnetlem4 ) FGDAFJZHJZB
      KIJTKLIHMZKGJZUAKLUBNOSNOPLFGMZIHBUACEUAQUCQR $.
      $( [24-Nov-2013] $) $( [12-Dec-2009] $)
  $}

$( (End of Jeffrey Hankins' mathbox.) $)


