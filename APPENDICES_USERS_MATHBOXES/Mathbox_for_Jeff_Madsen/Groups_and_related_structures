$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Groups and related structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    exidcl.1 $e |- X = ran G $.
    $( Closure of the binary operation of a magma with identity.  (Contributed
       by Jeff Madsen, 16-Jun-2011.) $)
    exidcl $p |- ( ( G e. ( Magma i^i ExId ) /\ A e. X /\ B e. X )
                                            -> ( A G B ) e. X ) $=
      ( cmagm cexid cin w3a co cdm wa crn rngopid syl5eq eleq2d anbi12d pm5.32i
      wcel inss1 sseli eqid clmgm syl3an1 3expb sylbi 3impb 3ad2ant1 eleqtrrd
      wceq ) CFGHZSZADSZBDSZIABCJZCKKZDULUMUNUOUPSZULUMUNLZLULAUPSZBUPSZLZLUQUL
      URVAULUMUSUNUTULDUPAULDCMUPECNOZPULDUPBVBPQRULUSUTUQULCFSUSUTUQUKFCFGTUAA
      BCUPUPUBUCUDUEUFUGULUMDUPUJUNVBUHUI $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d G x $.  $d Y x $.  $d X x $.  $d U u x y $.  $d H u x y $.
    exidres.1 $e |- X = ran G $.
    exidres.2 $e |- U = ( Id ` G ) $.
    exidres.3 $e |- H = ( G |` ( Y X. Y ) ) $.
    $( Lemma for ~ exidres and ~ exidresid . $)
    exidreslem $p |- ( ( G e. ( Magma i^i ExId ) /\ Y C_ X /\ U e. Y ) ->
                              ( U e. dom dom H /\ A. x e. dom dom H
                            ( ( U H x ) = x /\ ( x H U ) = x ) ) ) $=
      ( wcel wss cdm co wceq wa wral cxp sylib syl5eq dmeqd cmagm cexid cin w3a
      cv cres dmeqi xpss12 anidms wfo wf opidon2 fof fdm sseq2d syl5ibr ssdmres
      3syl imp dmxpid syl6eq eleq2d biimp3ar cmpidelt sylan2 anassrs adantrl wb
      ssel2 oveqi eqeq1d ancoms anbi12d adantl mpbird ralrimiva 3adant3 raleqdv
      ovres 3impa jca ) CUAUBUCJZFEKZBFJZUDZBDLZLZJZBAUEZDMZWINZWIBDMZWINZOZAWG
      PZWBWCWHWDWBWCOZWGFBWPWGFFQZLZFWPWFWQWPWFCWQUFZLZWQDWSIUGZWPWQCLZKZWTWQNZ
      WBWCXCWCXCWBWQEEQZKZWCXFFEFEUHUIWBXBXEWQWBXEECUJXEECUKXBXENCEGULXEECUMXEE
      CUNURUOUPUSZWQCUQZRSTFUTZVAVBVCWEWOWNAFPZWBWCWDXJWPWDOWNAFWPWDWIFJZWNWPWD
      XKOZOWNBWICMZWINZWIBCMZWINZOZWPXKXQWDWBWCXKXQWCXKOWBWIEJXQFEWIVIWIBCEGHVD
      VEVFVGXLWNXQVHWPXLWKXNWMXPXLWJXMWIXLWJBWIWSMXMDWSBWIIVJBWIFFCVSSVKXLWLXOW
      IXKWDWLXONXKWDOWLWIBWSMXODWSWIBIVJWIBFFCVSSVLVKVMVNVOVFVPVTWEWNAWGFWEWGWR
      FWEWFWQWEWFWTWQXAWEXCXDWBWCXCWDXGVQXHRSTXIVAVRVOWA $.
      $( [23-Dec-2013] $) $( [8-Jun-2010] $)

    $( The restriction of a binary operation with identity to a subset
       containing the identity has an identity element.  (Contributed by Jeff
       Madsen, 8-Jun-2010.) $)
    exidres $p |- ( ( G e. ( Magma i^i ExId ) /\ Y C_ X /\ U e. Y )
                                                      -> H e. ExId ) $=
      ( vu vx cexid wcel cv co wceq wa cdm wral eqeq1d cvv cmagm cin exidreslem
      wss w3a wrex oveq1 oveq2 anbi12d ralbidv rcla4ev syl wb cxp cres syl5eqel
      resexg eqid isexid 3ad2ant1 mpbird ) BUAKUBZLZEDUDZAELZUEZCKLZIMZJMZCNZVI
      OZVIVHCNZVIOZPZJCQQZRZIVOUFZVFAVOLAVICNZVIOZVIACNZVIOZPZJVORZPVQJABCDEFGH
      UCVPWCIAVOVHAOZVNWBJVOWDVKVSVMWAWDVJVRVIVHAVICUGSWDVLVTVIVHAVICUHSUIUJUKU
      LVCVDVGVQUMZVEVCCTLWEVCCBEEUNZUOTHBWFVBUQUPIJTCVOVOURUSULUTVA $.
      $( [23-Dec-2013] $) $( [8-Jun-2010] $)

    $( The restriction of a binary operation with identity to a subset
       containing the identity has the same identity element.  (Contributed by
       Jeff Madsen, 8-Jun-2010.) $)
    exidresid $p |- ( ( ( G e. ( Magma i^i ExId ) /\ Y C_ X /\ U e. Y ) /\
                                        H e. Magma ) -> ( Id ` H ) = U ) $=
      ( vu vx cmagm cexid wcel wa cv co wceq wral cvv adantr vy cin wss w3a cgi
      cfv crn crio cxp cres resexg syl5eqel eqid idrval syl 3ad2ant1 exidreslem
      cdm simprd exidres elin rngopid sylbir ancoms sylan raleqdv mpbird simpld
      wreu wb eleqtrrd exidu1 ax-17 a17d eqeq1d anbi12d ralbidv riota2f syl2anc
      oveq1 oveq2 mpbid eqtrd ) BKLUBZMZEDUCZAEMZUDZCKMZNZCUEUFZIOZJOZCPZWMQZWM
      WLCPZWMQZNZJCUGZRZIWSUHZAWHWKXAQZWIWEWFXBWGWECSMXBWECBEEUIZUJSHBXCWDUKULJ
      ISWKCWSWSUMZWKUMUNUOUPTWJAWMCPZWMQZWMACPZWMQZNZJWSRZXAAQZWJXJXIJCURURZRZW
      HXMWIWHAXLMZXMJABCDEFGHUQZUSTWJXIJWSXLWHCLMZWIWSXLQZABCDEFGHUTZWIXPXQWIXP
      NZCWDMZXQCKLVAZCVBVCVDVEZVFVGWJAWSMZWTIWSVIZXJXKVJWJAXLWSWHXNWIWHXNXMXOVH
      TYBVKWHXPWIYDXRWIXPYDXSXTYDYAJICWSXDVLVCVDVEWTXJIUAWSAUAOAMIVMYCXJIVNWLAQ
      ZWRXIJWSYEWOXFWQXHYEWNXEWMWLAWMCVTVOYEWPXGWMWLAWMCWAVOVPVQVRVSWBWC $.
      $( [23-Dec-2013] $) $( [8-Jun-2010] $)
  $}

  ${
    abl4pnp.1 $e |- X = ran G $.
    abl4pnp.2 $e |- D = ( /g ` G ) $.
    $( A commutative/associative law for Abelian groups.  (Contributed by Jeff
       Madsen, 11-Jun-2010.) $)
    abl4pnp $p |- ( ( G e. AbelOp /\ ( ( A e. X /\ B e. X ) /\
                  ( C e. X /\ F e. X ) ) ) -> ( ( A G B ) D ( C G F ) ) =
                                            ( ( A D C ) G ( B D F ) ) ) $=
      ( cabl wcel wa co wceq w3a 3expib anim1d 3anass imp syldan df-3an cgr syl
      ablmuldiv sylan2br adantrrr oveq1d wi grpocl syl6ibr abldivdiv4 grpodivcl
      ablogrpo an4 3imtr4g grpomuldivass sylan 3eqtr3d ) FJKZAGKZBGKZLZCGKZEGKZ
      LZLZLZABFMZCDMZEDMZACDMZBFMZEDMZVHCEFMDMZVKBEDMFMZVGVIVLEDUSVBVCVIVLNZVDV
      BVCLUSUTVAVCOVPUTVAVCUAABCDFGHIUDUEUFUGUSVFVHGKZVCVDOZVJVNNUSVFVRUSVFVQVE
      LVRUSVBVQVEUSFUBKZVBVQUHFUMZVSUTVAVQABFGHUIPUCQVQVCVDRUJSVHCEDFGHIUKTUSVS
      VFVMVONZVTVSVFVKGKZVAVDOZWAVSVFWCVSUTVCLZVAVDLZLWBWELVFWCVSWDWBWEVSUTVCWB
      ACDFGHIULPQUTVAVCVDUNWBVAVDRUOSVKBEDFGHIUPTUQUR $.
      $( [11-Jun-2010] $)
  $}

  ${
    grpeqdivid.1 $e |- X = ran G $.
    grpeqdivid.2 $e |- U = ( Id ` G ) $.
    grpeqdivid.3 $e |- D = ( /g ` G ) $.
    $( Two group elements are equal iff their quotient is the identity.
       (Contributed by Jeff Madsen, 6-Jan-2011.) $)
    grpoeqdivid $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
                                              ( A = B <-> ( A D B ) = U ) ) $=
      ( cgr wcel w3a wceq grpodivid 3adant2 oveq1 eqeq1d syl5ibrcom grponpcan
      co grpolid eqeq12d syl5ib impbid ) EJKZAFKZBFKZLZABMZABCTZDMZUHUKUIBBCTZD
      MZUEUGUMUFBCDEFGIHNOUIUJULDABBCPQRUKUJBETZDBETZMUHUIUJDBEPUHUNAUOBABCEFGI
      SUEUGUOBMUFBDEFGHUAOUBUCUD $.
      $( [6-Jan-2011] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.  $d X x y $.
    ghomf.1 $e |- X = ran G $.
    ghomf.2 $e |- W = ran H $.
    $( Mapping property of a group homomorphism.  (Contributed by Jeff Madsen,
       1-Dec-2009.) $)
    ghomf $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpHom H ) )
                                                  -> F : X --> W ) $=
      ( vx vy cgr wcel cghom co wf wa cv cfv wceq wral elghom simprbda 3impa )
      BJKZCJKZABCLMKZEDANZUCUDOUEUFHPZAQIPZAQCMUGUHBMAQRIESHESHIABCDEFGTUAUB $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d S u v x y $.  $d T u v x y $.  $d G u v x y $.  $d H u v x y $.
    $d K u v x y $.
    $( The composition of two group homomorphisms is a group homomorphism.
       (Contributed by Jeff Madsen, 1-Dec-2009.) $)
    ghomco $p |- ( ( ( G e. GrpOp /\ H e. GrpOp /\ K e. GrpOp ) /\
                    ( S e. ( G GrpHom H ) /\ T e. ( H GrpHom K ) ) ) ->
                                      ( T o. S ) e. ( G GrpHom K ) ) $=
      ( vx vy vu vv cgr wcel cghom co wa cv cfv wceq wral wi adantlr w3a crn wf
      fco ancoms ad2ant2r a1i ffvelrn adantrr adantrl fveq2 oveq1d oveq1 fveq2d
      jca eqeq12d oveq2d oveq2 rcla42v imp sylan an32s adantllr sylan9eq anasss
      ccom wfun cdm ffun ad2antlr ad2antrr fdm eleq2d biimpar fvco syl3anc eqid
      oveq12d grpocl 3expb sylan2 anassrs 3eqtr4d ralimdva com23 com12 3ad2ant1
      expr ex jcad wb elghom 3adant3 3adant1 anbi12d 3adant2 3imtr4d ) CJKZDJKZ
      EJKZUAZACDLMKZBDELMKZNZBAVFZCELMKZXACUBZDUBZAUCZFOZAPZGOZAPZDMZXJXLCMZAPZ
      QZGXGRZFXGRZNZXHEUBZBUCZHOZBPZIOZBPZEMZYCYEDMZBPZQZIXHRHXHRZNZNZXGYAXEUCZ
      XJXEPZXLXEPZEMZXOXEPZQZGXGRZFXGRZNZXDXFXAYMYNUUAYMYNSXAXIYBYNXSYKYBXIYNXG
      XHYABAUDUEUFUGWRWSYMUUASWTYMWRUUAXIYLXSWRUUASZXIYLNXSUUCXIYBYKXSUUCSXIYBN
      ZYKNZWRXSUUAUUEWRXSUUASZUUDWRYKUUFUUDWRNZYKNZXRYTFXGUUHXJXGKZNXQYSGXGUUHU
      UIXLXGKZXQYSSUUHUUIUUJNZXQYSUUHUUKXQNNXKBPZXMBPZEMZXPBPZYQYRUUHUUKXQUUNUU
      OQUUHUUKNXQUUNXNBPZUUOUUDYKUUKUUNUUPQZWRXIYKUUKUUQYBXIUUKYKUUQXIUUKNZXKXH
      KZXMXHKZNZYKUUQUURUUSUUTXIUUIUUSUUJXGXHXJAUHUIXIUUJUUTUUIXGXHXLAUHUJUOUVA
      YKUUQYJUUQUULYFEMZXKYEDMZBPZQHIXKXMXHXHYCXKQZYGUVBYIUVDUVEYDUULYFEYCXKBUK
      ULUVEYHUVCBYCXKYEDUMUNUPYEXMQZUVBUUNUVDUUPUVFYFUUMUULEYEXMBUKUQUVFUVCXNBY
      EXMXKDURUNUPUSUTVAVBVCVCXNXPBUKVDVEUUGUUKYQUUNQZYKXQUUDUUKUVGWRUUDUUKNYOU
      ULYPUUMEUUDUUIYOUULQZUUJUUDUUINBVGZAVGZXJAVHZKZUVHYBUVIXIUUIXHYABVIZVJXIU
      VJYBUUIXGXHAVIZVKXIUUIUVLYBXIUVLUUIXIUVKXGXJXGXHAVLZVMVNTXJBAVOVPUIUUDUUJ
      YPUUMQZUUIUUDUUJNUVIUVJXLUVKKZUVPYBUVIXIUUJUVMVJXIUVJYBUUJUVNVKXIUUJUVQYB
      XIUVQUUJXIUVKXGXLUVOVMVNTXLBAVOVPUJVRTUFUUGUUKYRUUOQZYKXQUUDWRUUKUVRWRUUK
      NUUDXOXGKZUVRWRUUIUUJUVSXJXLCXGXGVQZVSVTUUDUVSNUVIUVJXOUVKKZUVRYBUVIXIUVS
      UVMVJXIUVJYBUVSUVNVKXIUVSUWAYBXIUWAUVSXIUVKXGXOUVOVMVNTXOBAVOVPWAWBUFWCWH
      WBWDWDVBWIWEVEUTVBWFWGWJXAXBXTXCYLWRWSXBXTWKWTFGACDXHXGUVTXHVQZWLWMWSWTXC
      YLWKWRHIBDEYAXHUWBYAVQZWLWNWOWRWTXFUUBWKWSFGXECEYAXGUVTUWCWLWPWQUT $.
      $( [1-Dec-2009] $)
  $}

  ${
    ghomdiv.1 $e |- X = ran G $.
    ghomdiv.2 $e |- D = ( /g ` G ) $.
    ghomdiv.3 $e |- C = ( /g ` H ) $.
    $( Group homomorphisms preserve division.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    ghomdiv $p |- ( ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpHom H ) ) /\
                                ( A e. X /\ B e. X ) ) ->
                      ( F ` ( A D B ) ) = ( ( F ` A ) C ( F ` B ) ) ) $=
      ( cgr wcel co wa cfv wceq ffvelrn sylan 3ad2antl1 cghom w3a crn simpl2 wf
      ghomf adantrr adantrl grponpcan syl3anc 3expb fveq2d grpodivcl simprr jca
      eqid ghomlin eqcomd syldan 3eqtr2rd wb grporcan syl13anc mpbid ) FLMZGLMZ
      EFGUANMZUBZAHMZBHMZOZOZABDNZEPZBEPZGNZAEPZVOCNZVOGNZQZVNVRQZVLVSVQVMBFNZE
      PZVPVLVFVQGUCZMZVOWDMZVSVQQVEVFVGVKUDZVHVIWEVJVHHWDEUEZVIWEEFGWDHIWDUPZUF
      ZHWDAERSUGZVHVJWFVIVHWHVJWFWJHWDBERSUHZVQVOCGWDWIKUIUJVLWBAEVEVFVKWBAQZVG
      VEVIVJWMABDFHIJUIUKTULVHVKVMHMZVJOZWCVPQVEVFVKWOVGVEVKOWNVJVEVIVJWNABDFHI
      JUMUKZVEVIVJUNUOTVHWOOVPWCVMBEFGHIUQURUSUTVLVFVNWDMZVRWDMZWFVTWAVAWGVHVKW
      NWQVEVFVKWNVGWPTVHWHWNWQWJHWDVMERSUSVLVFWEWFWRWGWKWLVQVOCGWDWIKUMUJWLVNVR
      VOGWDWIVBVCVD $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d G x y $.  $d H x y $.  $d F x y $.  $d U x y $.  $d W x y $.
    $d X x y $.
    grpkerinj.1 $e |- X = ran G $.
    grpkerinj.2 $e |- W = ( Id ` G ) $.
    grpkerinj.3 $e |- Y = ran H $.
    grpkerinj.4 $e |- U = ( Id ` H ) $.
    $( A group homomorphism is injective if and only if its kernel is zero.
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    grpokerinj $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpHom H ) ) ->
                      ( F : X -1-1-> Y <-> ( `' F " { U } ) = { W } ) ) $=
      ( vx vy wcel co csn cima wceq wa cfv cgr cghom w3a wf1 ccnv ghomid wfn wf
      sneqd ghomf ffn syl grpoidcl 3ad2ant1 fnsnfv syl2anc eqtr3d imaeq2d snssd
      adantl wss f1imacnv sylan2 eqtrd expcom cv wral adantr cgs simpl2 ffvelrn
      wi wb adantrr adantrl eqid grpoeqdivid syl3anc adantlr ghomdiv eqeq1d cgi
      sylan cvv fvex eqeltri snid eleq1 wfun cdm ffun grpodivcl 3expb 3ad2antl1
      mpbiri eleqtrrd fvimacnv eleq2 sylan9bb an32s biimprd syl5 sylbid sylbird
      fdm elsni ralrimivva dff13 sylanbrc ex impbid ) CUANZDUANZBCDUBONZUCZFGBU
      DZBUEZAPZQZEPZRZXPXOYAXPXOSXSXQBXTQZQZXTXOXSYCRXPXOXRYBXQXOEBTZPZXRYBXOYD
      AAEBCDIKUFUIXOBFUGZEFNZYEYBRXOFGBUHZYFBCDGFHJUJZFGBUKULXLXMYGXNECFHIUMZUN
      FEBUOUPUQURUTXOXPXTFVAZYCXTRXLXMYKXNXLEFYJUSUNFGXTBVBVCVDVEXOYAXPXOYASZYH
      LVFZBTZMVFZBTZRZYMYORZVLZMFVGLFVGXPXOYHYAYIVHYLYSLMFFYLYMFNZYOFNZSZSZYQYN
      YPDVITZOZARZYRXOUUBYQUUFVMZYAXOUUBSZXMYNGNZYPGNZUUGXLXMXNUUBVJXOYTUUIUUAX
      OYHYTUUIYIFGYMBVKWCVNXOUUAUUJYTXOYHUUAUUJYIFGYOBVKWCVOYNYPUUDADGJKUUDVPZV
      QVRVSUUCUUFYMYOCVITZOZBTZARZYRUUCUUNUUEAXOUUBUUNUUERYAYMYOUUDUULBCDFHUULV
      PZUUKVTVSWAUUOUUNXRNZUUCYRUUOUUQAXRNAADWBTWDKDWBWEWFWGUUNAXRWHWOUUCUUQUUM
      XTNZYRXOUUBYAUUQUURVMUUHUUQUUMXSNZYAUURUUHBWIZUUMBWJZNUUQUUSVMXOUUTUUBXOY
      HUUTYIFGBWKULVHUUHUUMFUVAXLXMUUBUUMFNZXNXLYTUUAUVBYMYOUULCFHUUPWLWMWNXOUV
      AFRZUUBXOYHUVCYIFGBXEULVHWPUUMXRBWQUPXSXTUUMWRWSWTXOUUBUURYRVLYAUURUUMERZ
      UUHYRUUMEXFXLXMUUBUVDYRVLZXNXLYTUUAUVEXLYTUUAUCYRUVDYMYOUULECFHIUUPVQXAWM
      WNXBVSXCXBXDXCXGLMFGBXHXIXJXK $.
      $( [16-Jun-2011] $)
  $}

