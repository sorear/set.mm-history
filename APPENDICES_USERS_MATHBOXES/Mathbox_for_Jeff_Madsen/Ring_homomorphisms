$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ring homomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c RngHom $.
  $c RngIso $.
  $c ~=r $.

  $( Extend class notation with the class of ring homomorphisms. $)
  crnghom $a class RngHom $.

  $( Extend class notation with the class of ring isomorphisms. $)
  crngiso $a class RngIso $.

  $( Extend class notation with the ring isomorphism relation. $)
  crisc $a class ~=r $.

  ${
    $d r s t f x y $.
    $( Define the function which gives the set of ring homomorphisms between
       two given rings. $)
    df-rngohom $a |- RngHom = { <. <. r , s >. , t >. | ( ( r e. RingOps /\
         s e. RingOps ) /\ t = { f | ( f : ran ( 1st ` r ) --> ran ( 1st ` s )
                  /\ ( f ` ( Id ` ( 2nd ` r ) ) ) = ( Id ` ( 2nd ` s ) )
                    /\ A. x e. ran ( 1st ` r ) A. y e. ran ( 1st ` r )
            ( ( f ` ( x ( 1st ` r ) y ) ) = ( ( f ` x ) ( 1st ` s ) ( f ` y ) )
          /\ ( f ` ( x ( 2nd ` r ) y ) ) = ( ( f ` x ) ( 2nd ` s ) ( f ` y ) )
                                                          ) ) } ) } $.
  $}

  ${
    $d R r s t f x y $.  $d S r s t f x y $.  $d X r s t f x y $.
    $d Y s t f y $.  $d G r s t $.  $d H r s t $.  $d J s t $.  $d K s t $.
    $d U r s t $.  $d V s t $.
    rnghomval.1 $e |- G = ( 1st ` R ) $.
    rnghomval.2 $e |- H = ( 2nd ` R ) $.
    rnghomval.3 $e |- X = ran G $.
    rnghomval.4 $e |- U = ( Id ` H ) $.
    rnghomval.5 $e |- J = ( 1st ` S ) $.
    rnghomval.6 $e |- K = ( 2nd ` S ) $.
    rnghomval.7 $e |- Y = ran J $.
    rnghomval.8 $e |- V = ( Id ` K ) $.
    $( The set of ring homomorphisms.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    rngohomval $p |- ( ( R e. RingOps /\ S e. RingOps ) -> ( R RngHom S ) =
            { f | ( f : X --> Y /\ ( f ` U ) = V /\ A. x e. X A. y e. X
              ( ( f ` ( x G y ) ) = ( ( f ` x ) J ( f ` y ) ) /\
            ( f ` ( x H y ) ) = ( ( f ` x ) K ( f ` y ) ) ) ) } ) $=
      ( vr vs vt crngo cv c1st cfv crn wf c2nd cgi wceq co wral w3a cab crnghom
      wa cvv wcel fvex eqeltri rnex mapex mp2an simp1 ssexi fveq2 syl6eqr rneqd
      ss2abi feq2d fveq2d eqeq1d anbi12d raleqbidv 3anbi123d abbidv wb feq3 syl
      oveqd eqeq2d 2ralbidv df-rngohom ov2 ) UBUCUDCDUEUEUBUFZUGUHZUIZUCUFZUGUH
      ZUIZFUFZUJZWHUKUHZULUHZWNUHZWKUKUHZULUHZUMZAUFZBUFZWIUNZWNUHZXBWNUHZXCWNU
      HZWLUNZUMZXBXCWPUNZWNUHZXFXGWSUNZUMZUSZBWJUOZAWJUOZUPZFUQLMWNUJZEWNUHZKUM
      ZXBXCGUNZWNUHZXFXGIUNZUMZXBXCHUNZWNUHZXFXGJUNZUMZUSZBLUOALUOZUPZFUQZURLWM
      WNUJZXSWTUMZYBXHUMZYFXLUMZUSZBLUOZALUOZUPZFUQYLXRFUQZLUTVAMUTVAUUAUTVALGU
      IZUTPGGCUGUHZUTNCUGVBVCVDVCMIUIZUTTIIDUGUHZUTRDUGVBVCVDVCLMUTUTFVEVFYKXRF
      XRXTYJVGVLVHWHCUMZXQYTFUUFWOYMXAYNXPYSUUFWJLWMWNUUFWJUUBLUUFWIGUUFWIUUCGW
      HCUGVINVJZVKPVJZVMUUFWRXSWTUUFWQEWNUUFWQHULUHEUUFWPHULUUFWPCUKUHHWHCUKVIO
      VJZVNQVJVNVOUUFXOYRAWJLUUHUUFXNYQBWJLUUHUUFXIYOXMYPUUFXEYBXHUUFXDYAWNUUFW
      IGXBXCUUGWCVNVOUUFXKYFXLUUFXJYEWNUUFWPHXBXCUUIWCVNVOVPVQVQVRVSWKDUMZYTYKF
      UUJYMXRYNXTYSYJUUJWMMUMYMXRVTUUJWMUUDMUUJWLIUUJWLUUEIWKDUGVIRVJZVKTVJWMML
      WNWAWBUUJWTKXSUUJWTJULUHKUUJWSJULUUJWSDUKUHJWKDUKVISVJZVNUAVJWDUUJYQYIABL
      LUUJYOYDYPYHUUJXHYCYBUUJWLIXFXGUUKWCWDUUJXLYGYFUUJWSJXFXGUULWCWDVPWEVRVSA
      BUDFUCUBWFWG $.
      $( [19-Jun-2010] $)

    $d F x y f $.  $d G f $.  $d H f $.  $d J f $.  $d K f $.  $d U f $.
    $d V f $.
    $( The predicate "is a ring homomorphism from ` R ` to ` S ` ."
       (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    isrngohom $p |- ( ( R e. RingOps /\ S e. RingOps )
       -> ( F e. ( R RngHom S ) <->
                ( F : X --> Y /\ ( F ` U ) = V /\ A. x e. X A. y e. X
              ( ( F ` ( x G y ) ) = ( ( F ` x ) J ( F ` y ) ) /\
            ( F ` ( x H y ) ) = ( ( F ` x ) K ( F ` y ) ) ) ) ) ) $=
      ( vf crngo wcel wa crnghom co cfv wceq wral w3a cab rngohomval eleq2d cvv
      cv wf crn c1st fvex eqeltri rnex mpan2 3ad2ant1 feq1 fveq1 eqeq1d oveq12d
      fex eqeq12d anbi12d 2ralbidv 3anbi123d elab3 syl6bb ) CUCUDDUCUDUEZFCDUFU
      GZUDFLMUBUPZUQZEVRUHZKUIZAUPZBUPZGUGZVRUHZWBVRUHZWCVRUHZIUGZUIZWBWCHUGZVR
      UHZWFWGJUGZUIZUEZBLUJALUJZUKZUBULZUDLMFUQZEFUHZKUIZWDFUHZWBFUHZWCFUHZIUGZ
      UIZWJFUHZXBXCJUGZUIZUEZBLUJALUJZUKZVPVQWQFABCDEUBGHIJKLMNOPQRSTUAUMUNWPXK
      UBFWRWTFUOUDZXJWRLUOUDXLLGURUOPGGCUSUHUONCUSUTVAVBVALMUOFVIVCVDVRFUIZVSWR
      WAWTWOXJLMVRFVEXMVTWSKEVRFVFVGXMWNXIABLLXMWIXEWMXHXMWEXAWHXDWDVRFVFXMWFXB
      WGXCIWBVRFVFZWCVRFVFZVHVJXMWKXFWLXGWJVRFVFXMWFXBWGXCJXNXOVHVJVKVLVMVNVO
      $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d X x y $.  $d Y y $.  $d F x y $.
    rnghomf.1 $e |- G = ( 1st ` R ) $.
    rnghomf.2 $e |- X = ran G $.
    rnghomf.3 $e |- J = ( 1st ` S ) $.
    rnghomf.4 $e |- Y = ran J $.
    $( A ring homomorphism is a function.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    rngohomf $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                                          -> F : X --> Y ) $=
      ( vx vy crngo wcel co wa cfv wceq eqid crnghom wf c2nd cgi wral isrngohom
      cv w3a biimpa simp1d 3impa ) ANOZBNOZCABUAPOZFGCUBZULUMQZUNQUOAUCRZUDRZCR
      BUCRZUDRZSZLUGZMUGZDPCRVBCRZVCCRZEPSVBVCUQPCRVDVEUSPSQMFUELFUEZUPUNUOVAVF
      UHLMABURCDUQEUSUTFGHUQTIURTJUSTKUTTUFUIUJUK $.
      $( [19-Jun-2010] $)

    $( Closure law for a ring homomorphism.  (Contributed by Jeff Madsen,
       3-Jan-2011.) $)
    rngohomcl $p |- ( ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                          /\ A e. X ) -> ( F ` A ) e. Y ) $=
      ( crngo wcel crnghom co w3a wf cfv rngohomf ffvelrn sylan ) BMNCMNDBCOPNQ
      GHDRAGNADSHNBCDEFGHIJKLTGHADUAUB $.
      $( [3-Jan-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d F x y $.
    rnghom1.1 $e |- H = ( 2nd ` R ) $.
    rnghom1.2 $e |- U = ( Id ` H ) $.
    rnghom1.3 $e |- K = ( 2nd ` S ) $.
    rnghom1.4 $e |- V = ( Id ` K ) $.
    $( A ring homomorphism preserves ` 1 ` .  (Contributed by Jeff Madsen,
       24-Jun-2011.) $)
    rngohom1 $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                                  -> ( F ` U ) = V ) $=
      ( vx vy crngo wcel co cfv wceq wa eqid crnghom c1st crn wf wral isrngohom
      cv w3a biimpa simp2d 3impa ) ANOZBNOZDABUAPOZCDQGRZULUMSZUNSAUBQZUCZBUBQZ
      UCZDUDZUOLUGZMUGZUQPDQVBDQZVCDQZUSPRVBVCEPDQVDVEFPRSMURUELURUEZUPUNVAUOVF
      UHLMABCDUQEUSFGURUTUQTHURTIUSTJUTTKUFUIUJUK $.
      $( [24-Jun-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d F x y $.  $d G x y $.  $d J x y $.
    $d X x y $.  $d A x y $.  $d B y $.
    rnghomadd.1 $e |- G = ( 1st ` R ) $.
    rnghomadd.2 $e |- X = ran G $.
    rnghomadd.3 $e |- J = ( 1st ` S ) $.
    $( Ring homomorphisms preserve addition.  (Contributed by Jeff Madsen,
       3-Jan-2011.) $)
    rngohomadd $p |- ( ( ( R e. RingOps /\ S e. RingOps
        /\ F e. ( R RngHom S ) ) /\ ( A e. X /\ B e. X ) )
                    -> ( F ` ( A G B ) ) = ( ( F ` A ) J ( F ` B ) ) ) $=
      ( vx vy wcel co cfv wceq wral wa eqid crngo crnghom w3a cv c2nd isrngohom
      crn wf cgi biimpa simp3d 3impa simpl ralimi syl oveq1 fveq2d fveq2 oveq1d
      eqeq12d oveq2 oveq2d rcla42v mpan9 ) CUANZDUANZECDUBONZUCZLUDZMUDZFOZEPZV
      IEPZVJEPZGOZQZMHRZLHRZAHNBHNSABFOZEPZAEPZBEPZGOZQZVHVPVIVJCUEPZOEPVMVNDUE
      PZOQZSZMHRZLHRZVRVEVFVGWJVEVFSZVGSHGUGZEUHZWEUIPZEPWFUIPZQZWJWKVGWMWPWJUC
      LMCDWNEFWEGWFWOHWLIWETJWNTKWFTWLTWOTUFUJUKULWIVQLHWHVPMHVPWGUMUNUNUOVPWDA
      VJFOZEPZWAVNGOZQLMABHHVIAQZVLWRVOWSWTVKWQEVIAVJFUPUQWTVMWAVNGVIAEURUSUTVJ
      BQZWRVTWSWCXAWQVSEVJBAFVAUQXAVNWBWAGVJBEURVBUTVCVD $.
      $( [3-Jan-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d F x y $.  $d H x y $.  $d K x y $.
    $d X x y $.  $d A x y $.  $d B y $.
    rnghommul.1 $e |- G = ( 1st ` R ) $.
    rnghommul.2 $e |- X = ran G $.
    rnghommul.3 $e |- H = ( 2nd ` R ) $.
    rnghommul.4 $e |- K = ( 2nd ` S ) $.
    $( Ring homomorphisms preserve multiplication.  (Contributed by Jeff
       Madsen, 3-Jan-2011.) $)
    rngohommul $p |- ( ( ( R e. RingOps /\ S e. RingOps
      /\ F e. ( R RngHom S ) ) /\ ( A e. X /\ B e. X ) )
                   -> ( F ` ( A H B ) ) = ( ( F ` A ) K ( F ` B ) ) ) $=
      ( vx vy wcel co cfv wceq wral crngo crnghom w3a cv c1st crn cgi isrngohom
      wa wf eqid biimpa simp3d 3impa simpr ralimi syl oveq1 fveq2d fveq2 oveq1d
      eqeq12d oveq2 oveq2d rcla42v mpan9 ) CUAPZDUAPZECDUBQPZUCZNUDZOUDZGQZERZV
      KERZVLERZHQZSZOITZNITZAIPBIPUIABGQZERZAERZBERZHQZSZVJVKVLFQERVOVPDUERZQSZ
      VRUIZOITZNITZVTVGVHVIWKVGVHUIZVIUIIWGUFZEUJZGUGRZERHUGRZSZWKWLVIWNWQWKUCN
      OCDWOEFGWGHWPIWMJLKWOUKWGUKMWMUKWPUKUHULUMUNWJVSNIWIVROIWHVRUOUPUPUQVRWFA
      VLGQZERZWCVPHQZSNOABIIVKASZVNWSVQWTXAVMWREVKAVLGURUSXAVOWCVPHVKAEUTVAVBVL
      BSZWSWBWTWEXBWRWAEVLBAGVCUSXBVPWDWCHVLBEUTVDVBVEVF $.
      $( [3-Jan-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d G x y $.  $d J x y $.  $d F x y $.
    rnggrphom.1 $e |- G = ( 1st ` R ) $.
    rnggrphom.2 $e |- J = ( 1st ` S ) $.
    $( A ring homomorphism is a group homomorphism.  (Contributed by Jeff
       Madsen, 2-Jan-2011.) $)
    rngogrphom $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                                  -> F e. ( G GrpHom J ) ) $=
      ( vx vy crngo wcel co crn cv cfv wral wa cgr rngogrpo eqid crnghom w3a wf
      cghom wceq wb elghom syl2an 3adant3 rngohomf rngohomadd eqcomd ralrimivva
      mpbir2and ) AJKZBJKZCABUALKZUBZCDEUDLKZDMZEMZCUCZHNZCOINZCOELZVCVDDLCOZUE
      ZIUTPHUTPZUOUPUSVBVHQUFZUQUODRKERKVIUPADFSBEGSHICDEVAUTUTTZVATZUGUHUIABCD
      EUTVAFVJGVKUJURVGHIUTUTURVCUTKVDUTKQQVFVEVCVDABCDEUTFVJGUKULUMUN $.
      $( [2-Jan-2011] $)
  $}

  ${
    rnghom0.1 $e |- G = ( 1st ` R ) $.
    rnghom0.2 $e |- Z = ( Id ` G ) $.
    rnghom0.3 $e |- J = ( 1st ` S ) $.
    rnghom0.4 $e |- W = ( Id ` J ) $.
    $( A ring homomorphism preserves ` 0 ` .  (Contributed by Jeff Madsen,
       2-Jan-2011.) $)
    rngohom0 $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                                      -> ( F ` Z ) = W ) $=
      ( crngo wcel crnghom co w3a cgr cghom cfv rngogrpo wceq 3ad2ant1 3ad2ant2
      rngogrphom ghomid syl3anc ) ALMZBLMZCABNOMZPDQMZEQMZCDEROMGCSFUAUGUHUJUIA
      DHTUBUHUGUKUIBEJTUCABCDEHJUDFGCDEIKUEUF $.
      $( [2-Jan-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d F x y $.  $d H x y $.  $d K x y $.
    $d X x y $.  $d A x y $.  $d B y $.
    rnghomsub.1 $e |- G = ( 1st ` R ) $.
    rnghomsub.2 $e |- X = ran G $.
    rnghomsub.3 $e |- H = ( /g ` G ) $.
    rnghomsub.4 $e |- J = ( 1st ` S ) $.
    rnghomsub.5 $e |- K = ( /g ` J ) $.
    $( Ring homomorphisms preserve subtraction.  (Contributed by Jeff Madsen,
       15-Jun-2011.) $)
    rngohomsub $p |- ( ( ( R e. RingOps /\ S e. RingOps
       /\ F e. ( R RngHom S ) ) /\ ( A e. X /\ B e. X ) )
                   -> ( F ` ( A H B ) ) = ( ( F ` A ) K ( F ` B ) ) ) $=
      ( crngo wcel co w3a cfv crnghom cgr cghom wceq rngogrpo 3ad2ant1 3ad2ant2
      wa rngogrphom 3jca ghomdiv sylan ) CPQZDPQZECDUARQZSZFUBQZHUBQZEFHUCRQZSA
      JQBJQUHABGRETAETBETIRUDUPUQURUSUMUNUQUOCFKUEUFUNUMURUODHNUEUGCDEFHKNUIUJA
      BIGEFHJLMOUKUL $.
      $( [15-Jun-2011] $)
  $}

  ${
    $d R x y $.  $d S x y $.  $d T x y $.  $d F x y $.  $d G x y $.
    $( The composition of two ring homomorphisms is a ring homomorphism.
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    rngohomco $p |- ( ( ( R e. RingOps /\ S e. RingOps /\ T e. RingOps ) /\
                          ( F e. ( R RngHom S ) /\ G e. ( S RngHom T ) ) )
                                      -> ( G o. F ) e. ( R RngHom T ) ) $=
      ( vx vy wcel w3a co wa cfv wceq eqid 3expa 3adantl3 fvco3 wi ex imp crngo
      crnghom ccom c1st crn wf c2nd cgi cv wb isrngohom 3adant2 adantr rngohomf
      wral 3adantl1 adantrl adantrr fco syl2anc wfun ffun syl 3ad2ant1 rngohom1
      rngo1cl syl3anc fveq2d 3eqtrd adantlrr rngohomcl anim12da adantlrl syldan
      rngohomadd eqtrd rngogcl 3expb 3ad2antl1 adantlr jca sylan oveq12 3eqtr4d
      rngohommul rngocl ralrimivva mpbir3and ) AUAHZBUAHZCUAHZIZDABUBJHZEBCUBJH
      ZKZKZEDUCZACUBJHZAUDLZUEZCUDLZUEZWQUFZAUGLZUHLZWQLZCUGLZUHLZMZFUIZGUIZWSJ
      ZWQLZXJWQLZXKWQLZXAJZMZXJXKXDJZWQLZXNXOXGJZMZKZGWTUOFWTUOZWLWRXCXIYCIUJZW
      OWIWKYDWJFGACXEWQWSXDXAXGXHWTXBWSNZXDNZWTNZXENZXANZXGNZXBNZXHNZUKULUMWPBU
      DLZUEZXBEUFZWTYNDUFZXCWLWNYOWMWJWKWNYOWIWJWKWNYOBCEYMXAYNXBYMNZYNNZYIYKUN
      OUPUQZWLWMYPWNWIWJWMYPWKWIWJWMYPABDWSYMWTYNYEYGYQYRUNOPURZWTYNXBEDUSUTWPX
      FXEDLZELZBUGLZUHLZELZXHWPEVAZYPXEWTHZXFUUBMWPYOUUFYSYNXBEVBVCZYTWLUUGWOWI
      WJUUGWKAXEXDWTYGYFYHVFVDUMWTYNXEEDQVGWPUUAUUDEWLWMUUAUUDMZWNWIWJWMUUIWKWI
      WJWMUUIABXEDXDUUCUUDYFYHUUCNZUUDNZVEOPURVHWLWNUUEXHMZWMWJWKWNUULWIWJWKWNU
      ULBCUUDEUUCXGXHUUJUUKYJYLVEOUPUQVIWPYBFGWTWTWPXJWTHZXKWTHZKZKZXQYAUUPXLDL
      ZELZXJDLZELZXKDLZELZXAJZXMXPUUPUURUUSUVAYMJZELZUVCUUPUUQUVDEWLWMUUOUUQUVD
      MZWNWLWMKZUUOUVFWIWJWMUUOUVFRZWKWIWJWMUVHWIWJWMIZUUOUVFXJXKABDWSYMWTYEYGY
      QVOSOPTVJVHWPUUOUUSYNHZUVAYNHZKZUVEUVCMZWLWMUUOUVLWNUVGUUOUVLWIWJWMUUOUVL
      RZWKWIWJWMUVNUVIUUOUVLUVIUUMUUNUVJUVKXJABDWSYMWTYNYEYGYQYRVKXKABDWSYMWTYN
      YEYGYQYRVKVLSOPTVJZWLWNUVLUVMWMWLWNKZUVLUVMWJWKWNUVLUVMRZWIWJWKWNUVQWJWKW
      NIZUVLUVMUUSUVABCEYMXAYNYQYRYIVOSOUPTVMVNVPWPUUOXLWTHZXMUURMZWLUUOUVSWOWI
      WJUUOUVSWKWIUUMUUNUVSXJXKAWSWTYEYGVQVRVSVTWPUUFYPKZUVSUVTWPUUFYPUUHYTWAZU
      UFYPUVSUVTWTYNXLEDQOWBVNUUPXNUUTMZXOUVBMZKZXPUVCMWPUUMUUNUWCUWDWPUWAUUMUW
      CUWBUUFYPUUMUWCWTYNXJEDQOWBWPUWAUUNUWDUWBUUFYPUUNUWDWTYNXKEDQOWBVLZXNUUTX
      OUVBXAWCVCWDUUPXRDLZELZUUTUVBXGJZXSXTUUPUWHUUSUVAUUCJZELZUWIUUPUWGUWJEWLW
      MUUOUWGUWJMZWNUVGUUOUWLWIWJWMUUOUWLRZWKWIWJWMUWMUVIUUOUWLXJXKABDWSXDUUCWT
      YEYGYFUUJWESOPTVJVHWPUUOUVLUWKUWIMZUVOWLWNUVLUWNWMUVPUVLUWNWJWKWNUVLUWNRZ
      WIWJWKWNUWOUVRUVLUWNUUSUVABCEYMUUCXGYNYQYRUUJYJWESOUPTVMVNVPWPUUOXRWTHZXS
      UWHMZWLUUOUWPWOWIWJUUOUWPWKWIUUMUUNUWPXJXKAWSXDWTYEYFYGWFVRVSVTWPUWAUWPUW
      QUWBUUFYPUWPUWQWTYNXREDQOWBVNUUPUWEXTUWIMUWFXNUUTXOUVBXGWCVCWDWAWGWH $.
      $( [16-Jun-2011] $)
  $}

  ${
    rngkerinj.1 $e |- G = ( 1st ` R ) $.
    rngkerinj.2 $e |- X = ran G $.
    rngkerinj.3 $e |- W = ( Id ` G ) $.
    rngkerinj.4 $e |- J = ( 1st ` S ) $.
    rngkerinj.5 $e |- Y = ran J $.
    rngkerinj.6 $e |- Z = ( Id ` J ) $.
    $( A ring homomorphism is injective if and only if its kernel is zero.
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    rngokerinj $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
        -> ( F : X -1-1-> Y <-> ( `' F " { Z } ) = { W } ) ) $=
      ( wcel cfv crn eqtri cgi crngo crnghom co w3a c1st cgr cghom wf1 ccnv csn
      cima wceq wb eqid rngogrpo 3ad2ant1 3ad2ant2 rngogrphom fveq2i grpokerinj
      rneqi syl3anc ) AUAPZBUAPZCABUBUCPZUDAUEQZUFPZBUEQZUFPZCVFVHUGUCPGHCUHCUI
      IUJUKFUJULUMVCVDVGVEAVFVFUNZUOUPVDVCVIVEBVHVHUNZUOUQABCVFVHVJVKURICVFVHFG
      HGDRVFRKDVFJVASFDTQVFTQLDVFTJUSSHERVHRNEVHMVASIETQVHTQOEVHTMUSSUTVB $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d r s t f $.
    $( Define the function which gives the set of ring isomorphisms between two
       given rings. $)
    df-rngoiso $a |- RngIso = { <. <. r , s >. , t >. |
              ( ( r e. RingOps /\ s e. RingOps ) /\ t = { f e. ( r RngHom s ) |
                  f : ran ( 1st ` r ) -1-1-onto-> ran ( 1st ` s ) } ) } $.
  $}

  ${
    $d R r s t f $.  $d S r s t f $.  $d X r s t $.  $d Y s t $.
    rngisoval.1 $e |- G = ( 1st ` R ) $.
    rngisoval.2 $e |- X = ran G $.
    rngisoval.3 $e |- J = ( 1st ` S ) $.
    rngisoval.4 $e |- Y = ran J $.
    $( The set of ring isomorphisms.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    rngoisoval $p |- ( ( R e. RingOps /\ S e. RingOps ) -> ( R RngIso S ) =
                          { f e. ( R RngHom S ) | f : X -1-1-onto-> Y } ) $=
      ( vr vs cv c1st cfv crn crnghom wceq syl6eqr crngo wf1o crab crngiso ovex
      vt co rabex oveq1 wb fveq2 rneqd f1oeq2 rabeqbidv oveq2 f1oeq3 df-rngoiso
      syl ov2 ) LMUFABUAUALNZOPZQZMNZOPZQZCNZUBZCUTVCRUGZUCFGVFUBZCABRUGZUCUDFV
      EVFUBZCAVCRUGZUCVICVJABRUEUHUTASZVGVKCVHVLUTAVCRUIVMVBFSVGVKUJVMVBDQFVMVA
      DVMVAAOPDUTAOUKHTULITVBFVEVFUMURUNVCBSZVKVICVLVJVCBARUOVNVEGSVKVIUJVNVEEQ
      GVNVDEVNVDBOPEVCBOUKJTULKTVEGFVFUPURUNUFCMLUQUS $.
      $( [16-Jun-2011] $)

    $d X f $.  $d Y f $.  $d F f $.
    $( The predicate "is a ring isomorphism between ` R ` and ` S ` ."
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    isrngoiso $p |- ( ( R e. RingOps /\ S e. RingOps ) -> ( F e. ( R RngIso S )
      <-> ( F e. ( R RngHom S ) /\ F : X -1-1-onto-> Y ) ) ) $=
      ( vf crngo wcel wa crngiso co cv wf1o crnghom eleq2d f1oeq1 elrab syl6bb
      crab rngoisoval ) AMNBMNOZCABPQZNCFGLRZSZLABTQZUEZNCUKNFGCSZOUGUHULCABLDE
      FGHIJKUFUAUJUMLCUKFGUICUBUCUD $.
      $( [16-Jun-2011] $)

    $( A ring isomorphism is a bijection.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    rngoiso1o $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngIso S ) )
                                          -> F : X -1-1-onto-> Y ) $=
      ( crngo wcel crngiso co wf1o wa crnghom isrngoiso simplbda 3impa ) ALMZBL
      MZCABNOMZFGCPZUBUCQUDCABROMUEABCDEFGHIJKSTUA $.
      $( [16-Jun-2011] $)
  $}

  $( A ring isomorphism is a ring homomorphism.  (Contributed by Jeff Madsen,
     16-Jun-2011.) $)
  rngoisohom $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngIso S ) )
                                          -> F e. ( R RngHom S ) ) $=
    ( crngo wcel crngiso co crnghom c1st cfv wf1o eqid isrngoiso simprbda 3impa
    wa crn ) ADEZBDEZCABFGEZCABHGEZRSPTUAAIJZQZBIJZQZCKABCUBUDUCUEUBLUCLUDLUELM
    NO $.
    $( [16-Jun-2011] $)

  ${
    $d R x y $.  $d S x y $.  $d F x y $.
    $( The inverse of a ring isomorphism is a ring isomorphism.  (Contributed
       by Jeff Madsen, 16-Jun-2011.) $)
    rngoisocnv $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngIso S ) )
                                          -> `' F e. ( S RngIso R ) ) $=
      ( vx vy wcel co wa cfv wceq eqid ancoms sylan2 f1ocnvfv2 adantll f1ocnvdm
      wb wi 3expb sylan crngo crngiso ccnv crnghom c1st crn wf1o wf c2nd cgi cv
      wral w3a isrngohom adantr f1ocnv f1of syl ad2antll rngohom1 3expa adantrr
      rngo1cl f1ocnvfv ad2ant2rl mpd anim12da rngohomadd exp32 impr imp rngogcl
      oveq12 an32s adantlll adantlrl 3eqtr4rd wf1 f1of1 ad2antlr anassrs f1fveq
      adantllr syl12anc rngohommul rngocl jca ralrimivva mpbir3and ex isrngoiso
      mpbid 3imtr4d 3impia ) AUAFZBUAFZCABUBGFZCUCZBAUBGFZWOWPHZCABUDGFZAUEIZUF
      ZBUEIZUFZCUGZHZWRBAUDGFZXEXCWRUGZHZWQWSWTXGXJWTXGHZXHXIXKXHXEXCWRUHZBUIIZ
      UJIZWRIAUIIZUJIZJZDUKZEUKZXDGZWRIZXRWRIZXSWRIZXBGZJZXRXSXMGZWRIZYBYCXOGZJ
      ZHZEXEULDXEULZWTXHXLXQYKUMQZXGWPWOYLDEBAXNWRXDXMXBXOXPXEXCXDKZXMKZXEKZXNK
      ZXBKZXOKZXCKZXPKZUNLUOXFXLWTXAXFXIXLXCXECUPZXEXCWRUQURUSXKXPCIXNJZXQWTXAU
      UBXFWOWPXAUUBABXPCXOXMXNYRYTYNYPUTVAVBWOXFUUBXQRZWPXAXFWOUUCWOXFXPXCFUUCA
      XPXOXCYSYRYTVCXCXEXPXNCVDMLVEVFXKYJDEXEXEXKXRXEFZXSXEFZHZHZYEYIUUGYACIZYD
      CIZJZYEUUGYBCIZYCCIZXDGZXTUUIUUHXGUUFUUMXTJZWTXFUUFUUNXAXFUUFHZUUKXRJZUUL
      XSJZHZUUNXFUUDUUEUUPUUQXCXEXRCNXCXEXSCNVGZUUKXRUULXSXDVMUROOXKUUFUUIUUMJZ
      WTXAXFUUFUUTRZWOWPXAXFUVARWOWPXAUMZXFUUFUUTUUOUVBYBXCFZYCXCFZHZUUTXFUUDUU
      EUVCUVDXCXEXRCPXCXEXSCPVGZYBYCABCXBXDXCYQYSYMVHMVIVAVJVKWTXFUUFUUHXTJZXAW
      PXFUUFUVGWOWPUUFXFUVGWPUUFHZXTXEFZXFUVGWPUUDUUEUVIXRXSBXDXEYMYOVLSZXFUVIU
      VGXCXEXTCNLTVNVOVPVQWTXFUUFUUJYEQZXAWTXFHUUFHZXCXECVRZYAXCFZYDXCFZUVKXFUV
      MWTUUFXCXECVSVTZWPXFUUFUVNWOWPUUFXFUVNUVHUVIXFUVNUVJXFUVIUVNXCXEXTCPLTVNV
      OWOXFUUFUVOWPWOXFUUFUVOUUOWOUVEUVOUVFWOUVCUVDUVOYBYCAXBXCYQYSVLSMWAWCXCXE
      YAYDCWBWDVPWLUUGYGCIZYHCIZJZYIUUGUUKUULXMGZYFUVRUVQXGUUFUVTYFJZWTXFUUFUWA
      XAUUOUURUWAUUSUUKXRUULXSXMVMUROOXKUUFUVRUVTJZWTXAXFUUFUWBRZWOWPXAXFUWCRUV
      BXFUUFUWBUUOUVBUVEUWBUVFYBYCABCXBXOXMXCYQYSYRYNWEMVIVAVJVKWTXFUUFUVQYFJZX
      AWPXFUUFUWDWOWPUUFXFUWDUVHYFXEFZXFUWDWPUUDUUEUWEXRXSBXDXMXEYMYNYOWFSZXFUW
      EUWDXCXEYFCNLTVNVOVPVQWTXFUUFUVSYIQZXAUVLUVMYGXCFZYHXCFZUWGUVPWPXFUUFUWHW
      OWPUUFXFUWHUVHUWEXFUWHUWFXFUWEUWHXCXEYFCPLTVNVOWOXFUUFUWIWPWOXFUUFUWIUUOW
      OUVEUWIUVFWOUVCUVDUWIYBYCAXBXOXCYQYRYSWFSMWAWCXCXEYGYHCWBWDVPWLWGWHWIXFXI
      WTXAUUAUSWGWJABCXBXDXCXEYQYSYMYOWKWPWOWSXJQBAWRXDXBXEXCYMYOYQYSWKLWMWN $.
      $( [16-Jun-2011] $)
  $}

  ${
    $( The composition of two ring isomorphisms is a ring isomorphism.
       (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    rngoisoco $p |- ( ( ( R e. RingOps /\ S e. RingOps /\ T e. RingOps ) /\
                      ( F e. ( R RngIso S ) /\ G e. ( S RngIso T ) ) )
                                    -> ( G o. F ) e. ( R RngIso T ) ) $=
      ( crngo wcel crngiso co wa crnghom c1st cfv wf1o eqid rngoisohom 3adantl3
      crn 3expa 3adantl1 w3a ccom wb isrngoiso adantr anim12da rngohomco syldan
      3adant2 rngoiso1o adantrl adantrr f1oco syl2anc mpbir2and ) AFGZBFGZCFGZU
      AZDABHIGZEBCHIGZJZJZEDUBZACHIGZVDACKIGZALMZRZCLMZRZVDNZUSVEVFVKJUCZVBUPUR
      VLUQACVDVGVIVHVJVGOZVHOZVIOZVJOZUDUIUEUSVBDABKIGZEBCKIGZJVFUSUTVAVQVRUPUQ
      UTVQURUPUQUTVQABDPSQUQURVAVRUPUQURVAVRBCEPSTUFABCDEUGUHVCBLMZRZVJENZVHVTD
      NZVKUSVAWAUTUQURVAWAUPUQURVAWABCEVSVIVTVJVSOZVTOZVOVPUJSTUKUSUTWBVAUPUQUT
      WBURUPUQUTWBABDVGVSVHVTVMVNWCWDUJSQULVHVTVJEDUMUNUO $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d r s f $.
    $( Define the ring isomorphism relation. $)
    df-risc $a |- ~=r = { <. r , s >. | ( ( r e. RingOps /\ s e. RingOps )
                                  /\ E. f f e. ( r RngIso s ) ) } $.
  $}

  ${
    $d R r s f $.  $d S r s f $.
    $( The ring isomorphism relation.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    isriscg $p |- ( ( R e. A /\ S e. B ) -> ( R ~=r S <->
        ( ( R e. RingOps /\ S e. RingOps ) /\ E. f f e. ( R RngIso S ) ) ) ) $=
      ( vr vs cv crngo wcel wa crngiso co wex crisc eleq1 eleq2d exbidv anbi12d
      wceq anbi1d oveq1 anbi2d oveq2 df-risc brabg ) FHZIJZGHZIJZKZEHZUGUILMZJZ
      ENZKCIJZUJKZULCUILMZJZENZKUPDIJZKZULCDLMZJZENZKFGCDABOUGCTZUKUQUOUTVFUHUP
      UJUGCIPUAVFUNUSEVFUMURULUGCUILUBQRSUIDTZUQVBUTVEVGUJVAUPUIDIPUCVGUSVDEVGU
      RVCULUIDCLUDQRSEGFUEUF $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d R f $.  $d S f $.
    isrisc.1 $e |- R e. _V $.
    isrisc.2 $e |- S e. _V $.
    $( The ring isomorphism relation.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    isrisc $p |- ( R ~=r S <-> ( ( R e. RingOps /\ S e. RingOps )
                              /\ E. f f e. ( R RngIso S ) ) ) $=
      ( cvv wcel crisc wbr crngo wa cv crngiso co wex wb isriscg mp2an ) AFGBFG
      ABHIAJGBJGKCLABMNGCOKPDEFFABCQR $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d R f $.  $d S f $.
    $( The ring isomorphism relation.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    risc $p |- ( ( R e. RingOps /\ S e. RingOps ) ->
                      ( R ~=r S <-> E. f f e. ( R RngIso S ) ) ) $=
      ( crngo wcel wa crisc wbr cv crngiso co wex isriscg bianabs ) ADEBDEFABGH
      CIABJKECLDDABCMN $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d R f $.  $d S f $.  $d F f $.
    $( Determine that two rings are isomorphic.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    risci $p |- ( ( R e. RingOps /\ S e. RingOps /\
                                F e. ( R RngIso S ) ) -> R ~=r S ) $=
      ( vf crngo wcel crngiso co crisc wbr wa cv wex elex2 risc syl5ibr 3impia
      ) AEFZBEFZCABGHZFZABIJZUAUBRSKDLTFDMDCTNABDOPQ $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d f g r s t $.
    $( Ring isomorphism is an equivalence relation.  (Contributed by Jeff
       Madsen, 16-Jun-2011.) $)
    riscer $p |- Er ~=r $=
      ( vr vs vt vf vg crisc cv wbr crngo wcel wa crngiso wex vex isrisc 3expia
      co wi risci syld ccnv rngoisocnv ancoms exlimdv imp sylbi eeanv rngoisoco
      w3a ccom ex 3adant2 exlimdvv syl5bir 3expb adantlr an4s syl2anb iser ) AB
      CFAGZBGZFHZUTIJZVAIJZKZDGZUTVALQJZDMZKZVAUTFHZUTVADANBNZOZVEVHVJVEVGVJDVE
      VGVFUAZVAUTLQJZVJVCVDVGVNUTVAVFUBPVDVCVNVJRVDVCVNVJVAUTVMSPUCTUDUEUFVBVIV
      DCGZIJZKZEGZVAVOLQJZEMZKUTVOFHZVAVOFHVLVAVOEVKCNOVEVQVHVTWAVEVQKVHVTKZWAV
      CVQWBWARZVDVCVDVPWCWBVGVSKZEMDMVCVDVPUIZWAVGVSDEUGWEWDWADEWEWDVRVFUJZUTVO
      LQJZWAWEWDWGUTVAVOVFVRUHUKVCVPWGWARVDVCVPWGWAUTVOWFSPULTUMUNUOUPUEUQURUS
      $.
      $( [16-Jun-2011] $)
  $}

