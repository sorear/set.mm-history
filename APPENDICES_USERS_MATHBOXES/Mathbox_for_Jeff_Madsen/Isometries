$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Isometries
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Ismty $.
  $( Extend class notation with the class of metric space isometries. $)
  cismty $a class Ismty $.

  ${
    $d m n z f x y $.
    $( Define a function which takes two metric spaces and returns the set of
       isometries between the spaces.  An isometry is a bijection which
       preserves distance. $)
    df-ismty $a |- Ismty = { <. <. m , n >. , z >. | ( ( m e. Met /\ n e. Met )
        /\ z = { f | ( f : dom dom m -1-1-onto-> dom dom n /\ A. x e. dom dom m
            A. y e. dom dom m ( x m y ) = ( ( f ` x ) n ( f ` y ) ) ) } ) } $.
  $}

  ${
    $d M m n z f x y $.  $d N m n z f x y $.  $d X m n z f x y $.
    $d Y m n z f x y $.
    ismtyval.1 $e |- X = dom dom M $.
    ismtyval.2 $e |- Y = dom dom N $.
    $( The set of isometries between two metric spaces.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    ismtyval $p |- ( ( M e. Met /\ N e. Met ) -> ( M Ismty N ) =
                      { f | ( f : X -1-1-onto-> Y /\ A. x e. X A. y e. X
                            ( x M y ) = ( ( f ` x ) N ( f ` y ) ) ) } ) $=
      ( cme wcel cv co wceq wral wa cab cvv cdm dmexg vm vn wf1o cfv cismty wss
      vz wf f1of adantr ss2abi syl syl5eqel mapex syl2an ssexg sylancr wb dmeqd
      dmeq syl6eqr f1oeq2 oveq eqeq1d raleqbidv abbidv f1oeq3 2ralbidv df-ismty
      anbi12d eqeq2d ov2g mpd3an3 ) DJKZEJKZFGCLZUCZALZBLZDMZVRVPUDZVSVPUDZEMZN
      ZBFOAFOZPZCQZRKZDEUEMWGNVNVOPWGFGVPUHZCQZUFWJRKZWHWFWICVQWIWEFGVPUIUJUKVN
      FRKGRKWKVOVNFDSZSZRHVNWLRKWMRKDJTWLRTULUMVOGESZSZRIVOWNRKWORKEJTWNRTULUMF
      GRRCUNUOWGWJRUPUQUAUBUGDEJJUALZSZSZUBLZSZSZVPUCZVRVSWPMZWAWBWSMZNZBWROZAW
      ROZPZCQWGUEFXAVPUCZVTXDNZBFOZAFOZPZCQRWPDNZXHXMCXNXBXIXGXLXNWRFNXBXIURXNW
      RWMFXNWQWLWPDUTUSHVAZWRFXAVPVBULXNXFXKAWRFXOXNXEXJBWRFXOXNXCVTXDVRVSWPDVC
      VDVEVEVJVFWSENZXMWFCXPXIVQXLWEXPXAGNXIVQURXPXAWOGXPWTWNWSEUTUSIVAXAGFVPVG
      ULXPXJWDABFFXPXDWCVTWAWBWSEVCVKVHVJVFABUGCUAUBVIVLVM $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M f x y $.  $d N f x y $.  $d X f x y $.  $d Y f x y $.  $d F f x y $.
    isismty.1 $e |- X = dom dom M $.
    isismty.2 $e |- Y = dom dom N $.
    $( The condition "is an isometry".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isismty $p |- ( ( M e. Met /\ N e. Met ) -> ( F e. ( M Ismty N ) <->
                          ( F : X -1-1-onto-> Y /\ A. x e. X A. y e. X
                              ( x M y ) = ( ( F ` x ) N ( F ` y ) ) ) ) ) $=
      ( vf cme wcel wa co cv wf1o cfv wceq wral cvv cismty ismtyval eleq2d f1of
      cab wi wb wf cdm dmexg syl syl5eqel fex syl2an expcom adantr f1oeq1 fveq1
      adantrd oveq12d eqeq2d 2ralbidv anbi12d elab3g bitrd ) DKLZEKLZMZCDEUANZL
      CFGJOZPZAOZBOZDNZVLVJQZVMVJQZENZRZBFSAFSZMZJUEZLZFGCPZVNVLCQZVMCQZENZRZBF
      SAFSZMZVHVIWACABJDEFGHIUBUCVHWICTLZUFWBWIUGVHWCWJWHVFWCWJUFVGWCVFWJWCFGCU
      HFTLWJVFFGCUDVFFDUIZUIZTHVFWKTLWLTLDKUJWKTUJUKULFGTCUMUNUOUPUSVTWIJCTVJCR
      ZVKWCVSWHFGVJCUQWMVRWGABFFWMVQWFVNWMVOWDVPWEEVLVJCURVMVJCURUTVAVBVCVDUKVE
      $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M u v x y $.  $d N u v x y $.  $d F u v x y $.
    $( The inverse of an isometry is an isometry.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    ismtycnv $p |- ( ( M e. Met /\ N e. Met ) ->
                      ( F e. ( M Ismty N ) -> `' F e. ( N Ismty M ) ) ) $=
      ( vx vy vu vv cme wcel wa cdm wf1o cv co cfv wceq wral cismty wi adantr
      ccnv f1ocnv f1ocnvdm ex anim12d imdistani oveq1 fveq2 oveq1d oveq2 oveq2d
      eqeq12d rcla42v impcom adantll syl adantrr adantrl oveq12d adantlr eqtr2d
      f1ocnvfv2 ralrimivva jca a1i eqid isismty wb ancoms 3imtr4d ) BHIZCHIZJZB
      KKZCKKZALZDMZEMZBNZVQAOZVRAOZCNZPZEVNQDVNQZJZVOVNAUAZLZFMZGMZCNZWHWFOZWIW
      FOZBNZPZGVOQFVOQZJZABCRNIWFCBRNIZWEWPSVMWEWGWOVPWGWDVNVOAUBTWEWNFGVOVOWEW
      HVOIZWIVOIZJZJZWMWKAOZWLAOZCNZWJXAWEWKVNIZWLVNIZJZJWMXDPZWEWTXGVPWTXGSWDV
      PWRXEWSXFVPWRXEVNVOWHAUCUDVPWSXFVNVOWIAUCUDUETUFWDXGXHVPXGWDXHWCXHWKVRBNZ
      XBWACNZPDEWKWLVNVNVQWKPZVSXIWBXJVQWKVRBUGXKVTXBWACVQWKAUHUIULVRWLPZXIWMXJ
      XDVRWLWKBUJXLWAXCXBCVRWLAUHUKULUMUNUOUPVPWTXDWJPWDVPWTJXBWHXCWICVPWRXBWHP
      WSVNVOWHAVBUQVPWSXCWIPWRVNVOWIAVBURUSUTVAVCVDVEDEABCVNVOVNVFZVOVFZVGVLVKW
      QWPVHFGWFCBVOVNXNXMVGVIVJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y F $.  $d x y M $.  $d x y N $.  $d x y P $.  $d x R $.  $d x y X $.
    ismtyima.1 $e |- X = dom dom M $.
    $( The image of a ball under an isometry is another ball. $)
    ismtyima $p |- ( ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) ) /\
                       ( P e. X /\ R e. RR+ ) ) ->
               ( F " ( P ( ball ` M ) R ) ) = ( ( F ` P ) ( ball ` N ) R ) ) $=
      ( vx vy wcel co wa cfv wss wceq adantr syl syl3anc wb clt cme w3a crp cbl
      cismty cima cv cdm crn imassrn wf1o wral eqid isismty biimp3a simpld f1of
      wf frn syl5ss sseld simpl2 simprl ffvelrn syl2anc simprr blssm wbr simpl1
      ccnv simplrr simplrl f1ocnv 3syl sylan elbl2 syl22anc simprd oveq1 oveq1d
      fveq2 eqeq12d oveq2 oveq2d rcla42v impancom imp syldan breq1d bitrd f1of1
      wi f1elima f1ocnvfv2 simpr eqeltrd 3bitr4d eleq1d 3bitr3d pm5.21ndd eqrdv
      wf1 ex ) DUAJZEUAJZCDEUEKJZUBZAFJZBUCJZLZLZHCABDUDMKZUFZACMZBEUDMKZXKHUGZ
      EUHUHZJZXPXMJZXPXOJZXKXMXQXPXKXMCUIZXQCXLUJXKFXQCURZYAXQNXKFXQCUKZYBXKYCX
      PIUGZDKZXPCMZYDCMZEKZOZIFULHFULZXGYCYJLZXJXDXEXFYKHICDEFXQGXQUMZUNUOPZUPZ
      FXQCUQQZFXQCUSQUTVAXKXOXQXPXKXEXNXQJZXIXOXQNXDXEXFXJVBZXKYBXHYPYOXGXHXIVC
      ZFXQACVDVEZXGXHXIVFZEXNBXQYLVGRVAXKXRXSXTSXKXRLZXPCVJZMZCMZXMJZUUDXOJZXSX
      TUUAUUCXLJZXNUUDEKZBTVHZUUEUUFUUAUUGAUUCDKZBTVHZUUIUUAXDXIXHUUCFJZUUGUUKS
      XKXDXRXDXEXFXJVIZPXGXHXIXRVKZXGXHXIXRVLXKXQFUUBURZXRUULXKYCXQFUUBUKUUOYNF
      XQCVMXQFUUBUQVNXQFXPUUBVDVOZUUCDABFGVPVQUUAUUJUUHBTXKXRUULUUJUUHOZUUPXKUU
      LUUQXKXHYJUULUUQWLYRXKYCYJYMVRXHUULYJUUQYIUUQAYDDKZXNYGEKZOHIAUUCFFXPAOZY
      EUURYHUUSXPAYDDVSUUTYFXNYGEXPACWAVTWBYDUUCOZUURUUJUUSUUHYDUUCADWCUVAYGUUD
      XNEYDUUCCWAWDWBWEWFVEWGWHWIWJUUAFXQCXBZUULXLFNZUUEUUGSXKUVBXRXKYCUVBYNFXQ
      CWKQPUUPXKUVCXRXKXDXHXIUVCUUMYRYTDABFGVGRPFXQCUUCXLWMRUUAXEXIYPUUDXQJUUFU
      UISXKXEXRYQPUUNXKYPXRYSPUUAUUDXPXQXKYCXRUUDXPOYNFXQXPCWNVOZXKXRWOWPUUDEXN
      BXQYLVPVQWQUUAUUDXPXMUVDWRUUAUUDXPXOUVDWRWSXCWTXA $.
      $( [31-Jan-2014] $)
  $}

  ${
    $d u v w x y z F $.  $d u y z J $.  $d u K $.  $d u v w x y M $.
    $d u v w x y z N $.  $d u y z ph $.  $d v w X $.  $d v w x y Y $.
    ismtyhmeo.1 $e |- J = ( MetOpen ` M ) $.
    ismtyhmeo.2 $e |- K = ( MetOpen ` N ) $.
    ${
      ismtyhmeolem.3 $e |- X = dom dom M $.
      ismtyhmeolem.4 $e |- Y = dom dom N $.
      ismtyhmeolem.5 $e |- ( ph -> ( M e. Met /\ N e. Met ) ) $.
      ismtyhmeolem.6 $e |- ( ph -> F e. ( M Ismty N ) ) $.
      $( Lemma for ~ ismtyhmeo .  (The proof was shortened by Mario Carneiro,
         16-Jan-2014.) $)
      ismtyhmeolem $p |- ( ph -> A. u e. J ( F " u ) e. K ) $=
        ( vy wcel wss wa syl vx vz vw vv cv cima cbl cfv wrex wral wceq wfun wi
        crn wf wf1o co cismty cme isismty mpbid simpld f1of ffun adantr fvelima
        wb ex w3a crp opni2 syl3an1 simpl1 simprd simpl2 elssuni uniopn2 sseq2d
        cuni biimpar sylan2 syl2anc simpl3 sseldd ffvelrn simprl blelrn syl3anc
        blcntr sselda 3impa ismtyima syl32anc ad2antll eqsstr3d anbi12d rcla4ev
        imass2 eleq2 sseq1 syl12anc expr rexlimdva 3expa eleq1 anbi1d syl5ibcom
        mpd rexbidv syld ralrimiv wfo f1ofo forn imassrn sseq2 mpbii 3syl isopn
        jctild sylibrd ) ACBUEZUFZEQZBDAYBDQZYCIRZPUEZUAUEZQZYHYCRZSZUAGUGUHZUN
        ZUIZPYCUJZSZYDAYEYOYFAYEYOAYESZYNPYCYQYGYCQZUBUEZCUHZYGUKZUBYBUIZYNYQCU
        LZYRUUBUMAUUCYEAHICUOZUUCAHICUPZUUDAUUEUCUEZUDUEZFUQUUFCUHUUGCUHGUQUKUD
        HUJUCHUJZACFGURUQQZUUEUUHSZOAFUSQZGUSQZSUUIUUJVGNUCUDCFGHILMUTTVAVBZHIC
        VCTZHICVDTVEUUCYRUUBUBYGYBCVFVHTYQUUAYNUBYBYQYSYBQZSYTYHQZYJSZUAYMUIZUU
        AYNAYEUUOUURAYEUUOVIZYSYGFUGUHUQZYBRZPVJUIZUURAUUKYEUUOUVBAUUKUULNVBZPY
        BFYSDJVKVLUUSUVAUURPVJUUSYGVJQZUVAUURUUSUVDUVASZSZYTYGYLUQZYMQZYTUVGQZU
        VGYCRZUURUVFUULYTIQZUVDUVHUVFAUULAYEUUOUVEVMZAUUKUULNVNZTZUVFUUDYSHQZUV
        KUVFAUUDUVLUUNTUVFYBHYSUVFAYEYBHRZUVLAYEUUOUVEVOYEAYBDVSZRZUVPYBDVPAUVP
        UVRAHUVQYBAUUKHUVQUKUVCFDHLJVQTVRVTWAZWBAYEUUOUVEWCWDHIYSCWEWBZUUSUVDUV
        AWFZGYTYGIMWGWHUVFUULUVKUVDUVIUVNUVTUWAGYTYGIMWIWHUVFUVGCUUTUFZYCUVFUUK
        UULUUIUVOUVDUWBUVGUKUVFAUUKUVLUVCTUVNUVFAUUIUVLOTUUSUVOUVEAYEUUOUVOYQYB
        HYSUVSWJWKVEUWAYSYGCFGHLWLWMUVAUWBYCRUUSUVDUUTYBCWRWNWOUUQUVIUVJSUAUVGY
        MYHUVGUKUUPUVIYJUVJYHUVGYTWSYHUVGYCWTWPWQXAXBXCXHXDUUAUUQYKUAYMUUAUUPYI
        YJYTYGYHXEXFXIXGXCXJXKVHAUUEYFUUMUUEHICXLCUNZIUKZYFHICXMHICXNUWDYCUWCRY
        FCYBXOUWCIYCXPXQXRTXTAUULYDYPVGUVMPUAYCGEIMKXSTYAXK $.
        $( [16-Jan-2014] $) $( [2-Sep-2009] $)
    $}

    $( An isometry is a homeomorphism on the induced topology.  (Contributed by
       Jeff Madsen, 2-Sep-2009.)  (The proof was shortened by Mario Carneiro,
       19-Jan-2014.) $)
    ismtyhmeo $p |- ( ( M e. Met /\ N e. Met ) ->
                            ( F e. ( M Ismty N ) -> F e. ( J Homeo K ) ) ) $=
      ( vu vx vy cme wcel wa cismty co cv wral eqid cdm wceq chomeosm cuni wf1o
      cima ccnv w3a ctop opntop ishomeo syl2an adantr isismty simprbda ad2antrr
      wb uniopn2 ad2antlr f1oeq23 syl2anc mpbid simpl simpr ismtyhmeolem ancomd
      cfv ismtycnv imp mpbir3and ex ) DKLZEKLZMZADENOLZABCUAOLZVLVMMZVNBUBZCUBZ
      AUCZAHPZUDCLHBQZAUEZVSUDBLHCQZVLVNVRVTWBUFUOZVMVJBUGLCUGLWCVKDBFUHECGUHHA
      BCVPVQVPRVQRUIUJUKVODSSZESSZAUCZVRVLVMWFIPZJPZDOWGAVEWHAVEEOTJWDQIWDQIJAD
      EWDWEWDRZWERZULUMVOWDVPTZWEVQTZWFVRUOVJWKVKVMDBWDWIFUPUNVKWLVJVMECWEWJGUP
      UQWDVPWEVQAURUSUTVOHABCDEWDWEFGWIWJVLVMVAZVLVMVBVCVOHWACBEDWEWDGFWJWIVOVJ
      VKWMVDVLVMWAEDNOLADEVFVGVCVHVI $.
      $( [19-Jan-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d r w x y z F $.  $d r w x y z M $.  $d r w y z N $.
    $( Lemma for ~ ismtybnd .  (The proof was shortened by Mario Carneiro,
       19-Jan-2014.) $)
    ismtybndlem $p |- ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) )
                                              -> ( M e. Bnd -> N e. Bnd ) ) $=
      ( vx vr vy vz vw cme wcel co cdm cv cfv wceq crp wrex wral wa wi w3a cbnd
      cismty cbl ccnv wf1o eqid isismty biimp3a simpld f1ocnv f1of 3syl ffvelrn
      wf sylan oveq1 eqeq2d rexbidv rcla4v syl imaeq2 wfo f1ofo foima adantr ex
      cima anim1d imp syldan simpl f1ocnvfv2 syl2an oveq1d eqtrd eqeq12d syl5ib
      ismtyima anassrs reximdva syld ralrimdva isbnd2 3ad2ant1 3ad2ant2 3imtr4d
      wb ) BIJZCIJZABCUCKJZUAZBLLZDMZEMZBUDNZKZOZEPQZDWMRZCLLZFMZWOCUDNZKZOZEPQ
      ZFXARZBUBJZCUBJZWLWTXFFXAWLXBXAJZSZWTWMXBAUEZNZWOWPKZOZEPQZXFXKXMWMJZWTXP
      TWLXAWMXLUOZXJXQWLWMXAAUFZXAWMXLUFXRWLXSGMZHMZBKXTANYAANCKOHWMRGWMRZWIWJW
      KXSYBSGHABCWMXAWMUGZXAUGZUHUIUJZWMXAAUKXAWMXLULUMZXAWMXBXLUNZUPWSXPDXMWMW
      NXMOZWRXOEPYHWQXNWMWNXMWOWPUQURUSUTVAXKXOXEEPWLXJWOPJZXOXETXOAWMVHZAXNVHZ
      OWLXJYISZSZXEWMXNAVBYMYJXAYKXDWLYJXAOZYLWLXSWMXAAVCYNYEWMXAAVDWMXAAVEUMVF
      YMYKXMANZWOXCKZXDWLYLXQYISZYKYPOWLYLYQWLXJXQYIWLXRXJXQTYFXRXJXQYGVGVAVIVJ
      XMWOABCWMYCVSVKYMYOXBWOXCWLXSXJYOXBOYLYEXJYIVLWMXAXBAVMVNVOVPVQVRVTWAWBWC
      WIWJXHWTWHWKDBWMEYCWDWEWJWIXIXGWHWKFCXAEYDWDWFWG $.
      $( [19-Jan-2014] $) $( [2-Sep-2009] $)

    $( Isometries preserve boundedness.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    ismtybnd $p |- ( ( M e. Met /\ N e. Met /\ F e. ( M Ismty N ) )
                                              -> ( M e. Bnd <-> N e. Bnd ) ) $=
      ( cme wcel cismty co w3a cbnd ismtybndlem ismtycnv 3impia 3com12 syld3an3
      ccnv wi impbid ) BDEZCDEZABCFGEZHBIEZCIEZABCJRSTAOZCBFGEZUBUAPZRSTUDABCKL
      SRUDUEUCCBJMNQ $.
      $( [19-Jan-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d X u v x y $.  $d M u v x y $.  $d N u v x y $.  $d A u v x y $.
    $d B u v x y $.  $d S u v x y $.  $d T u v x y $.  $d F u v x y $.
    ismtyres.1 $e |- X = dom dom M $.
    ismtyres.2 $e |- B = ( F " A ) $.
    ismtyres.3 $e |- S = ( M |` ( A X. A ) ) $.
    ismtyres.4 $e |- T = ( N |` ( B X. B ) ) $.
    $( A restriction of an isometry is an isometry.  The condition ` A C_ X `
       is not necessary but makes the proof easier.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    ismtyres $p |- ( ( ( M e. Met /\ N e. Met ) /\ ( F e. ( M Ismty N )
                              /\ A C_ X ) ) -> ( F |` A ) e. ( S Ismty T ) ) $=
      ( cme wcel wa co cdm cfv wceq wb vx vy vu vv wss cismty cres wf1o cv wral
      wf1 f1of1 f1ores ancoms adantll cxp metssba2 dmeqi syl6eqr f1oeq2 adantlr
      cima syl adantr wf f1f crn imassrn frn syl5ss adantl eqid anassrs xpeq12i
      sylan2 reseq2i eqtri f1oeq3 adantlll bitrd ex syl5 adantrd eleq2d anbi12d
      mpbid wi ssel anim12d imp oveq1 fveq2 oveq1d eqeq12d oveq2 oveq2d rcla42v
      an32s adantlrl oveqi ovres syl5eq ad2antrl ad2antll oveq12d f1ofun sseq2d
      fvres f1odm biimparc funfvima2 syl2anc syl6eleqr adantrr adantrl adantlrr
      wfun eqtrd 3eqtr4d sylbird ralrimivv jcad isismty metres syl5eqel 3imtr4d
      syl2an impr ancom2s ) FMNZGMNZOZAHUEZEFGUFPNZEAUGZCDUFPNZYLYMYNYPYLYMOZHG
      QQZEUHZUAUIZUBUIZFPZYTERZUUAERZGPZSZUBHUJUAHUJZOZCQZQZDQZQZYOUHZUCUIZUDUI
      ZCPZUUNYORZUUOYORZDPZSZUDUUJUJUCUUJUJZOZYNYPYQUUHUUMUVAYQYSUUMUUGYSHYREUK
      ZYQUUMHYREULYQUVCUUMYQUVCOZAEAVBZYOUHZUUMYMUVCUVFYLUVCYMUVFHYRAEUMUNUOUVD
      UVFUUJUVEYOUHZUUMYQUVFUVGTZUVCYJYMUVHYKYJYMOZAUUJSUVHUVIAFAAUPUGZQZQUUJFH
      AIUQUUIUVKCUVJKURURUSZAUUJUVEYOUTVCVAVDYKYMUVCUVGUUMTZYJYKYMOUVCOZUVEUULS
      UVMUVNUVEGUVEUVEUPZUGZQZQZUULYKYMUVCUVEUVRSZYMUVCOYKUVEYRUEZUVSUVCYMHYREV
      EZUVTHYREVFUWAUVTYMUWAUVEEVGYREAVHHYREVIVJVKVOGYRUVEYRVLZUQVOVMUUKUVQDUVP
      DGBBUPZUGZUVPLUWCUVOGBUVEBUVEJJVNVPVQURURUSUVEUULUUJYOVRVCVSVTWFWAWBWCYQU
      UHUVAYQUUHOZUUTUCUDUUJUUJUWEUUNUUJNZUUOUUJNZOZUUNANZUUOANZOZUUTYQUWKUWHTZ
      UUHYJYMUWLYKUVIUWIUWFUWJUWGUVIAUUJUUNUVLWDUVIAUUJUUOUVLWDWEVAVDUWEUWKUUTU
      WEUWKOUUNUUOFPZUUNERZUUOERZGPZUUPUUSYMUUHUWKUWMUWPSZYLYMUUGUWKUWQYSYMUWKU
      UGUWQYMUWKOZUUGUWQUWRUUNHNZUUOHNZOZUUGUWQWGYMUWKUXAYMUWIUWSUWJUWTAHUUNWHA
      HUUOWHWIWJUUFUWQUUNUUAFPZUWNUUDGPZSUAUBUUNUUOHHYTUUNSZUUBUXBUUEUXCYTUUNUU
      AFWKUXDUUCUWNUUDGYTUUNEWLWMWNUUAUUOSZUXBUWMUXCUWPUUAUUOUUNFWOUXEUUDUWOUWN
      GUUAUUOEWLWPWNWQVCWJWRWSVSUWKUUPUWMSUWEUWKUUPUUNUUOUVJPUWMCUVJUUNUUOKWTUU
      NUUOAAFXAXBVKYMUUHUWKUUSUWPSZYLYMYSUWKUXFUUGYMYSOZUWKOZUUSUWNUWODPZUWPUXH
      UUQUWNUURUWODUWIUUQUWNSUXGUWJUUNAEXHXCUWJUURUWOSUXGUWIUUOAEXHXDXEUXHUXIUW
      NUWOUWDPZUWPDUWDUWNUWOLWTUXHUWNBNZUWOBNZUXJUWPSUXGUWIUXKUWJUXGUWIOUWNUVEB
      UXGUWIUWNUVENZUXGEXQZAEQZUEZUWIUXMWGYSUXNYMHYREXFVKZYSUXPYMYSUXOHAHYREXIX
      GXJZAUUNEXKXLWJJXMXNUXGUWJUXLUWIUXGUWJOUWOUVEBUXGUWJUWOUVENZUXGUXNUXPUWJU
      XSWGUXQUXRAUUOEXKXLWJJXMXOUWNUWOBBGXAXLXBXRXPVSXSWAXTYAWAYBYLYNUUHTYMUAUB
      EFGHYRIUWBYCVDYLYPUVBTZYMYJCMNDMNUXTYKYJCUVJMKFAYDYEYKDUWDMLGBYDYEUCUDYOC
      DUUJUULUUJVLUULVLYCYGVDYFYHYI $.
      $( [5-Jan-2014] $) $( [2-Sep-2009] $)
  $}

