$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Algebraic numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c AA $.

  $( Extend class notation to include the set of algebraic numbers. $)
  caa $a class AA $. $( Blackboard bold capital A $)

  $( Define the set of algebraic numbers.  An algebraic number is a root of a
     nonzero polynomial over the integers.  Here we construct it as the union
     of all kernels (preimages of ` { 0 } ` ) of all polynomials in
     ` ( Poly `` ZZ ) ` , except the zero polynomial ` ( CC X. { 0 } ) ` . $)
  df-aa $a |- AA =
    U_ f e. ( ( Poly ` ZZ ) \ { ( CC X. { 0 } ) } ) ( `' f " { 0 } ) $.

  ${
    $d f k m x y z A $.  $d i j k m n x y N $.  $d i j P $.  $d i j k m z ph $.
    $d f k m n z B $.  $d f i j m z F $.  $d i j k n x y K $.  $d f k m z R $.
    $( Elementhood in the set of algebraic numbers.  (Contributed by Mario
       Carneiro, 22-Jul-2014.) $)
    elaa $p |- ( A e. AA <-> ( A e. CC /\
      E. f e. ( ( Poly ` ZZ ) \ { ( CC X. { 0 } ) } ) ( f ` A ) = 0 ) ) $=
      ( caa wcel cz cply cfv cc cc0 csn cxp cdif cv ccnv cima ciun wrex wceq wa
      df-aa eleq2i eliun wfn wb wf eldifi plyf ffn 0cn fniniseg sylancl rexbiia
      3syl r19.42v bitri 3bitri ) ACDABEFGZHIJZKJZLZBMZNUROZPZDAVBDZBUTQZAHDZAV
      AGIRZBUTQSZCVCABTUABAUTVBUBVEVFVGSZBUTQVHVDVIBUTVAUTDZVAHUCZIHDVDVIUDVJVA
      UQDHHVAUEVKVAUQUSUFEVAUGHHVAUHUMUIHIAVAHUJUKULVFVGBUTUNUOUP $.
      $( [22-Jul-2014] $)

    $( An algebraic number is a complex number.  (Contributed by Mario
       Carneiro, 23-Jul-2014.) $)
    aacn $p |- ( A e. AA -> A e. CC ) $=
      ( vf caa wcel cc cv cfv cc0 wceq cz cply csn cxp cdif wrex elaa simplbi )
      ACDAEDABFGHIBJKGEHLMLNOABPQ $.
      $( [23-Jul-2014] $)

    $( The algebraic numbers are a subset of the complex numbers.  (Contributed
       by Mario Carneiro, 23-Jul-2014.) $)
    aasscn $p |- AA C_ CC $=
      ( vx caa cc cv aacn ssriv ) ABCADEF $.
      $( [23-Jul-2014] $)

    ${
      elqaa.1 $e |- ( ph -> A e. CC ) $.
      elqaa.2 $e |- ( ph -> F e. ( ( Poly ` QQ ) \ { ( CC X. { 0 } ) } ) ) $.
      elqaa.3 $e |- ( ph -> ( F ` A ) = 0 ) $.
      elqaa.4 $e |- B = ( coeff ` F ) $.
      elqaa.5 $e |- N = ( k e. NN0 |-> sup (
        { n e. NN | ( ( B ` k ) x. n ) e. ZZ } , RR , `' < ) ) $.
      elqaa.6 $e |- R = ( seq 0 ( x. , N ) ` ( deg ` F ) ) $.
      $( Lemma for ~ elqaa .  The function ` N ` represents the denominators of
         the rational coefficients ` B ` .  By multiplying them all together to
         make ` R ` , we get a number big enough to clear all the denominators
         and make ` R x. F ` an integer polynomial. $)
      elqaalem1 $p |- ( ( ph /\ K e. NN0 ) ->
        ( ( N ` K ) e. NN /\ ( ( B ` K ) x. ( N ` K ) ) e. ZZ ) ) $=
        ( wcel cz cn cr cq cn0 wa cfv cv cmul co crab clt ccnv csup wceq oveq1d
        fveq2 eleq1d rabbidv supeq1d wor ltso cnvso mpbi supex fvmpt adantl cuz
        c1 wss c0 wne ssrab2 nnuz sseqtri wrex wf cply cc0 csn cxp cdif eldifsn
        cc sylib simpld 0z zq ax-mp coef2 sylancl ffvelrn sylan qmulz syl rabn0
        sylibr infmssuzcl sylancr eqeltrd oveq2 elrab ) AHUAPZUBZHIUCZHCUCZFUDZ
        UEUFZQPZFRUGZPXARPXBXAUEUFZQPZUBWTXAXFSUHUIZUJZXFWSXAXJUKAEHEUDZCUCZXCU
        EUFZQPZFRUGZSXIUJXJUAIXKHUKZSXOXFXIXPXNXEFRXPXMXDQXPXLXBXCUEXKHCUMULUNU
        OUPNSXFXISUHUQSXIUQURSUHUSUTVAVBVCWTXFVEVDUCZVFXFVGVHZXJXFPXFRXQXEFRVIV
        JVKWTXEFRVLZXRWTXBTPZXSAUATCVMZWSXTAGTVNUCZPZVOTPZYAAYCGVTVOVPVQZVHZAGY
        BYEVPVRPYCYFUBKGYBYEVSWAWBVOQPYDWCVOWDWECTGMWFWGUATHCWHWIFXBWJWKXEFRWLW
        MXFVEWNWOWPXEXHFXARXCXAUKXDXGQXCXAXBUEWQUNWRWA $.
        $( [23-Jul-2014] $)

      ${
        elqaa.7 $e |- P = ( x e. _V , y e. _V |->
          ( ( x x. y ) mod ( N ` K ) ) ) $.
        $( Lemma for ~ elqaa . $)
        elqaalem2 $p |- ( ( ph /\ K e. ( 0 ... ( deg ` F ) ) ) ->
          ( R mod ( N ` K ) ) = 0 ) $=
          ( wcel vi vj vm cc0 cdgr cfv cfz co wa cmo cmpt cn0 cseq wceq elfznn0
          cn cv fveq2i nnmulcl adantl cz elqaalem1 simpld adantlr sylan2 cuz cq
          cmul cply cc csn cxp wne cdif eldifsn sylib eqid dgrcl nn0uz syl6eleq
          syl adantr cr crp ad2antll zmodcl syl2anc nn0re nnre ad2antrl modabs2
          nnrp modmul1 syl221anc recnd mulcom oveq1d 3eqtr4rd nn0z eqtr4d oveq1
          nnz ovex fvmpt oveq12d cvv oveq12 ovmpt2a mp2an 3eqtr4d fveq2 seqhomo
          syl6eq syl5eq a1i seqf ffvelrn syl5eqel vex nn0mulcl syl2anr crab clt
          wf 0z ccnv csup eleq1d rabbidv supeq1d eqtri fmptd syl2an elexi nn0cn
          cbvmptv mul02 0mod sylan9eqr c1 3syl mul01 simpr nnne0 divid syl6eqel
          cdiv nncn 1z wb mod0 mpbird eqtrd seqz 3eqtr3d ) AKUDJUEUFZUGUHZTZUIZ
          GHUPHUQZKLUFZUJUHZUKZUFZUUPFHULUUTLUFZUVAUJUHZUKZUDUMUFZGUVAUJUHZUDUU
          RAKULTZUVDUVHUNKUUPUOZAUVJUIZUVDUUPVHLUDUMZUFZUVCUFUVHGUVNUVCRURUVLUA
          UBVHFUPLUVGUVCUDUUPUAUQZUPTZUBUQZUPTZUIZUVOUVQVHUHZUPTZUVLUVOUVQUSZUT
          ZUVOUUQTZUVLUVOULTZUVOLUFZUPTZUVOUUPUOZAUWEUWGUVJAUWEUIUWGUVOEUFUWFVH
          UHVATADEGHIJUVOLMNOPQRVBVCZVDZVEAUUPUDVFUFZTUVJAUUPULUWKAJVGVIUFZTZUU
          PULTZAUWMJVJUDVKVLZVMZAJUWLUWOVKVNTUWMUWPUINJUWLUWOVOVPVCVGJUUPUUPVQV
          RWAZVSVTWBUVLUVSUIZUVTUVAUJUHZUVOUVAUJUHZUVQUVAUJUHZVHUHZUVAUJUHZUVTU
          VCUFZUVOUVCUFZUVQUVCUFZFUHZUWRUWSUVOUXAVHUHZUVAUJUHZUXCUWRUXAUVOVHUHZ
          UVAUJUHZUVQUVOVHUHZUVAUJUHZUXIUWSUWRUXAWCTZUVQWCTZUVOVATZUVAWDTZUXAUV
          AUJUHUXAUNZUXKUXMUNUWRUXAULTZUXNUWRUVQVATZUVAUPTZUXSUVRUXTUVLUVPUVQXB
          WEUVLUYAUVSUVLUYAKEUFUVAVHUHVATADEGHIJKLMNOPQRVBVCZWBZUVQUVAWFWGZUXAW
          HWAZUVRUXOUVLUVPUVQWIWEZUVPUXPUVLUVRUVOXBWJZUWRUYAUXQUYCUVAWLZWAZUWRU
          XOUXQUXRUYFUYIUVQUVAWKWGUXAUVQUVOUVAWMWNUWRUXHUXJUVAUJUWRUVOVJTZUXAVJ
          TUXHUXJUNUWRUVOUVPUVOWCTZUVLUVRUVOWIWJZWOZUWRUXAUYEWOUVOUXAWPWGWQUWRU
          VTUXLUVAUJUWRUYJUVQVJTUVTUXLUNUYMUWRUVQUYFWOUVOUVQWPWGWQWRUWRUWTWCTZU
          YKUXAVATZUXQUWTUVAUJUHUWTUNZUXCUXIUNUWRUWTULTZUYNUWRUXPUYAUYQUYGUYCUV
          OUVAWFWGUWTWHWAUYLUWRUXSUYOUYDUXAWSWAUYIUWRUYKUXQUYPUYLUYIUVOUVAWKWGU
          WTUVOUXAUVAWMWNWTUWRUWAUXDUWSUNUWCHUVTUVBUWSUPUVCUUTUVTUVAUJXAUVCVQZU
          VTUVAUJXCZXDWAUWRUXGUWTUXAFUHZUXCUWRUXEUWTUXFUXAFUVPUXEUWTUNUVLUVRHUV
          OUVBUWTUPUVCUUTUVOUVAUJXAUYRUVOUVAUJXCZXDWJUVRUXFUXAUNUVLUVPHUVQUVBUX
          AUPUVCUUTUVQUVAUJXAUYRUVQUVAUJXCZXDWEXEUWTXFTUXAXFTUYTUXCUNVUAVUBBCUW
          TUXAXFXFBUQZCUQZVHUHZUVAUJUHZUXCFVUCUWTUNVUDUXAUNUIVUEUXBUVAUJVUCUWTV
          UDUXAVHXGWQSUXBUVAUJXCXHXIXMXJUWDUVLUWEUWFUVCUFZUVOUVGUFZUNUWHUVLUWEU
          IZVUGUWFUVAUJUHZVUHVUIUWGVUGVUJUNUWJHUWFUVBVUJUPUVCUUTUWFUVAUJXAUYRUW
          FUVAUJXCZXDWAUWEVUHVUJUNUVLHUVOUVFVUJULUVGUUTUVOUNUVEUWFUVAUJUUTUVOLX
          KWQUVGVQZVUKXDUTWTVEXLXNVEUURAUVJUVDUVIUNZUVKUVLGUPTZVUMAVUNUVJAGUVNU
          PRAULUPUVMYDUWNUVNUPTAUAUBVHUPLUDULVSUDVATAYEXOUWIUVSUWAAUWBUTXPUWQUL
          UPUUPUVMXQWGXRWBHGUVBUVIUPUVCUUTGUVAUJXAUYRGUVAUJXCXDWAVEUUSUAUBFULUV
          GKUDUUPULUDUUSUWEUVQULTUIZUIUVOUVQFUHZUWSULUVOXFTZUVQXFTVUPUWSUNUAXSZ
          UBXSBCUVOUVQXFXFVUFUWSFVUCUVOUNZVUDUVQUNUIVUEUVTUVAUJVUCUVOVUDUVQVHXG
          WQSUYSXHXIVUOUVTVATZUYAUWSULTUUSVUOUVTULTVUTUVOUVQXTUVTWSWAUURAUVJUYA
          UVKUYBVEZUVTUVAWFYAXRUUSULULUVGYDZUWEVUHULTUWDUURAUVJVVBUVKUVLHULUVFU
          LUVGUVLUUTULTZUIZUVEVATZUYAUVFULTVVDUVEUPTZVVEAVVCVVFUVJAVVCUIVVFUUTE
          UFZUVEVHUHVATADEGUCIJUUTLMNOPLHULVVGIUQZVHUHZVATZIUPYBZWCYCYFZYGZUKUC
          ULUCUQZEUFZVVHVHUHZVATZIUPYBZWCVVLYGZUKQHUCULVVMVVSUUTVVNUNZWCVVKVVRV
          VLVVTVVJVVQIUPVVTVVIVVPVAVVTVVGVVOVVHVHUUTVVNEXKWQYHYIYJYPYKRVBVCVDUV
          EXBWAUVLUYAVVCUYBWBUVEUVAWFWGVULYLVEUWHULULUVOUVGXQYMUUSUWEUIZUDUVOFU
          HZUDUVOVHUHZUVAUJUHZUDUDXFTZVUQVWBVWDUNUDVAYEYNZVURBCUDUVOXFXFVUFVWDF
          VUCUDUNVUDUVOUNUIVUEVWCUVAUJVUCUDVUDUVOVHXGWQSVWCUVAUJXCXHXIUWEUUSVWD
          UDUVAUJUHZUDUWEVWCUDUVAUJUWEUYJVWCUDUNUVOYOZUVOYQWAWQUUSUYAUXQVWGUDUN
          VVAUYHUVAYRUUAZYSXNVWAUVOUDFUHZUVOUDVHUHZUVAUJUHZUDVUQVWEVWJVWLUNVURV
          WFBCUVOUDXFXFVUFVWLFVUSVUDUDUNUIVUEVWKUVAUJVUCUVOVUDUDVHXGWQSVWKUVAUJ
          XCXHXIUWEUUSVWLVWGUDUWEVWKUDUVAUJUWEUYJVWKUDUNVWHUVOUUBWAWQVWIYSXNAUU
          RUUCAUWNUURUWQWBUUSKUVGUFZUVAUVAUJUHZUDUUSUVJVWMVWNUNUURUVJAUVKUTHKUV
          FVWNULUVGUUTKUNUVEUVAUVAUJUUTKLXKWQVULUVAUVAUJXCXDWAUUSVWNUDUNZUVAUVA
          UUGUHZVATZUUSVWPYTVAUUSUVAVJTZUVAUDVMZVWPYTUNUUSUYAVWRVVAUVAUUHWAUUSU
          YAVWSVVAUVAUUDWAUVAUUEWGUUIUUFUUSUVAWCTZUXQVWOVWQUUJUUSUYAVWTVVAUVAWI
          WAUUSUYAUXQVVAUYHWAUVAUVAUUKWGUULUUMUUNUUO $.
          $( [23-Jul-2014] $)
      $}

      $( Lemma for ~ elqaa . $)
      elqaalem3 $p |- ( ph -> A e. AA ) $=
        ( cc wcel cc0 wceq cmul co vf vz vm vx vy cv cfv cply csn cxp cdif wrex
        cz caa cof wne cdgr cfz cexp csu cmpt cvv cnex a1i wa cseq fvex eqeltri
        fconstmpt wfn wf eldifsn sylib simpld plyf syl ffn dffn5v offval2 fzfid
        cq cn cn0 nn0uz 0z crab ssrab2 cr ccnv csup fveq2 oveq1d eleq1d rabbidv
        clt supeq1d wor ltso cnvso mpbi supex fvmpt adantl cuz wss nnuz sseqtri
        c1 c0 ax-mp coef2 sylancl ffvelrn sylan qmulz sylibr infmssuzcl sylancr
        zq eqeltrd sseldi nnmulcl seqf eqid syl2anc adantr sylan2 oveq2d mulass
        rabn0 nncn syl3anc 3eqtr4d mpteq2dva eqtrd cdiv nnne0 eqcomd sylanbrc
        cmo dgrcl syl5eqel elfznn0 coef3 expcl adantll mulcl fsummulc2 sumeq2dv
        coeid2 zsscn mulcom divcan2 divcl 3eqtrd oveq2 elrab cmpt2 elqaalem2 wb
        simprbi crp nnre nnrp mod0 syl2an mpbid zmulcl elplyd simprd oveq1 ovex
        divcan3 div0 0cn eqeq12d syl5ib necon3d mpd fconst inidm fvconst2 ofval
        mpdan mul01 fveq1 eqeq1d rcla4ev elaa ) ABOPZBUAUFZUGZQRZUAUMUHUGZOQUIU
        JZUIZUKZULZBUNPIAODUIZUJZGSUOTZUWQPZBUXAUGZQRZUWRAUXAUWNPUXAUWOUPZUXBAU
        XAUBOQGUQUGZURTZDUCUFZCUGZSTZUBUFZUXHUSTZSTZUCUTZVAZUWNAUXAUBODUXKGUGZS
        TZVAUXOAUBODUXPSUWTGVBVBVBOVBPAVCVDZDVBPAUXKOPZVEZDUXFSHQVFZUGZVBNUXFUY
        AVGVHZVDZUXPVBPUXTUXKGVGVDUWTUBODVARAUBODVIVDZAGOVJZGUBOUXPVAZRAOOGVKZU
        YFAGWAUHUGZPZUYHAUYJGUWOUPZAGUYIUWPUKPUYJUYKVEJGUYIUWOVLVMZVNZWAGVOVPZO
        OGVQVPZUBOGVRVMZVSZAUBOUXQUXNUXTDUXGUXIUXLSTZUCUTZSTUXGDUYRSTZUCUTUXQUX
        NUXTUXGUYRDUCUXTQUXFVTADOPZUXSADWBPZVUAADUYBWBNAWCWBUYAVKUXFWCPZUYBWBPA
        UCESWBHQWCWDQUMPZAWEVDAUXHWCPZVEZUXIFUFZSTZUMPZFWBWFZWBUXHHUGZVUIFWBWGZ
        VUFVUKVUJWHWOWIZWJZVUJVUEVUKVUNRAEUXHEUFZCUGZVUGSTZUMPZFWBWFZWHVUMWJVUN
        WCHVUOUXHRZWHVUSVUJVUMVUTVURVUIFWBVUTVUQVUHUMVUTVUPUXIVUGSVUOUXHCWKWLWM
        WNWPMWHVUJVUMWHWOWQWHVUMWQWRWHWOWSWTXAXBXCVUFVUJXHXDUGZXEVUJXIUPZVUNVUJ
        PVUJWBVVAVULXFXGVUFVUIFWBULZVVBVUFUXIWAPZVVCAWCWACVKZVUEVVDAUYJQWAPZVVE
        UYMVUDVVFWEQXSXJCWAGLXKXLWCWAUXHCXMXNFUXIXOVPVUIFWBYJXPVUJXHXQXRXTZYAZU
        XHWBPVUOWBPVEUXHVUOSTWBPAUXHVUOYBXCYCAUYJVUCUYMWAGUXFUXFYDZUUAVPZWCWBUX
        FUYAXMYEUUBZDYKVPZYFZUXHUXGPZUXTVUEUYROPZUXHUXFUUCZUXTVUEVEZUXIOPZUXLOP
        ZVVOUXTWCOCVKZVUEVVRAVVTUXSAUYJVVTUYMCWAGLUUDVPZYFWCOUXHCXMZXNZUXSVUEVV
        SAUXKUXHUUEUUFZUXIUXLUUGYEYGUUHUXTUXPUYSDSAUYJUXSUXPUYSRUYMCWAUCGUXFUXK
        VVILUUJXNYHUXTUXGUXMUYTUCVVNUXTVUEUXMUYTRZVVPVVQVUAVVRVVSVWEUXTVUAVUEVV
        MYFVWCVWDDUXIUXLYIYLYGUUIYMYNYOAUBUXJUMUCUXFUMOXEAUUKVDVVJAVVNVEZUXJUXI
        VUKSTZDVUKYPTZSTZUMVVNAVUEUXJVWIRVVPVUFUXJUXIDSTZUXIVUKVWHSTZSTZVWIVUFV
        UAVVRUXJVWJRAVUAVUEVVLYFZAVVTVUEVVRVWAVWBXNZDUXIUULYEVUFVWLVWJVUFVWKDUX
        ISVUFVUAVUKOPZVUKQUPZVWKDRVWMVUFVUKWBPZVWOVVHVUKYKVPZVUFVWQVWPVVHVUKYQV
        PZDVUKUUMYLYHYRVUFVWIVWLVUFVVRVWOVWHOPZVWIVWLRVWNVWRVUFVUAVWOVWPVWTVWMV
        WRVWSDVUKUUNYLUXIVUKVWHYIYLYRUUOYGVWFVWGUMPZVWHUMPZVWIUMPVVNAVUEVXAVVPV
        UFVUKVUJPZVXAVVGVXCVWQVXAVUIVXAFVUKWBVUGVUKRVUHVWGUMVUGVUKUXISUUPWMUUQU
        VAVPYGVWFDVUKYTTQRZVXBAUDUEBCUDUEVBVBUDUFUEUFSTVUKYTTUURZDEFGUXHHIJKLMN
        VXEYDUUSVWFVUBVWQVXDVXBUUTZAVUBVVNVVKYFVVNAVUEVWQVVPVVHYGVUBDWHPVUKUVBP
        VXFVWQDUVCVUKUVDDVUKUVEUVFYEUVGVWGVWHUVHYEXTUVIXTAUYKUXEAUYJUYKUYLUVJAU
        XAUWOGUWOUXAUWORUXAUWTYPUOZTZUWOUWTVXGTZRAGUWORUXAUWOUWTVXGUVKAVXHGVXIU
        WOAUBOUXQDYPTZVAUYGVXHGAUBOVXJUXPUXTUXPOPZVUADQUPZVXJUXPRAUYHUXSVXKUYNO
        OUXKGXMXNVVMAVXLUXSAVUBVXLVVKDYQVPZYFUXPDUVMYLYNAUBOUXQDYPUXAUWTVBVBVBU
        XRUXQVBPUXTDUXPSUVLVDUYDUYQUYEVSUYPYMAUBOQDYPTZVAUBOQVAZVXIUWOAUBOVXNQA
        VXNQRZUXSAVUAVXLVXPVVLVXMDUVNYEYFYNAUBOQDYPUWOUWTVBOVBUXRQOPUXTUVOVDUYD
        UWOVXORAUBOQVIVDZUYEVSVXQYMUVPUVQUVRUVSUXAUWNUWOVLYSAUXCDQSTZQAUWJUXCVX
        RRIAOODQSOUWTGVBVBBAOUWSUWTVKZUWTOVJVXSAODUYCUVTVDOUWSUWTVQVPUYOUXRUXRO
        UWAUWJBUWTUGDRAODBUYCUWBXCABGUGQRUWJKYFUWCUWDAVUAVXRQRVVLDUWEVPYOUWMUXD
        UAUXAUWQUWKUXARUWLUXCQBUWKUXAUWFUWGUWHYEBUAUWIYS $.
        $( [23-Jul-2014] $)
    $}

    $( The set of numbers generated by the roots of polynomials in the
       _rational numbers_ is the same as the set of algebraic numbers, which by
       ~ elaa are defined only in terms of polynomials over the integers.
       (Contributed by Mario Carneiro, 23-Jul-2014.) $)
    elqaa $p |- ( A e. AA <-> ( A e. CC /\
      E. f e. ( ( Poly ` QQ ) \ { ( CC X. { 0 } ) } ) ( f ` A ) = 0 ) ) $=
      ( vm vj vk vn wcel cc cv cfv cc0 wceq cq wa cz wss cmul co cn crab caa wi
      cply cdif wrex elaa zssq qsscn plyss mp2an ssdif ssrexv mp2b anim2i sylbi
      csn cxp ccoe cdgr cn0 cr clt ccnv csup cmpt cseq simpll simplr simpr eqid
      fveq2 oveq1d eleq1d rabbidv cbvrabv syl6eq supeq1d elqaalem3 ex rexlimdva
      oveq2 cbvmptv imp impbii ) AUAGZAHGZABIZJKLZBMUCJZHKUPUQUPZUDZUEZNZWEWFWH
      BOUCJZWJUDZUEZNWMABUFWPWLWFWNWIPZWOWKPWPWLUBOMPMHPWQUGUHOMUIUJWNWIWJUKWHB
      WOWKULUMUNUOWFWLWEWFWHWEBWKWFWGWKGZNZWHWEWSWHNAWGURJZWGUSJQCUTCIZWTJZDIZQ
      RZOGZDSTZVAVBVCZVDZVEZKVFJZEFWGXIWFWRWHVGWFWRWHVHWSWHVIWTVJCEUTXHEIZWTJZF
      IZQRZOGZFSTZVAXGVDXAXKLZVAXFXPXGXQXFXLXCQRZOGZDSTXPXQXEXSDSXQXDXROXQXBXLX
      CQXAXKWTVKVLVMVNXSXODFSXCXMLXRXNOXCXMXLQWAVMVOVPVQWBXJVJVRVSVTWCWD $.
      $( [23-Jul-2014] $)

    $( Every rational number is algebraic.  (Contributed by Mario Carneiro,
       23-Jul-2014.) $)
    qaa $p |- ( A e. QQ -> A e. AA ) $=
      ( vf vx vy cq wcel cc cv cfv cc0 wceq csn cmin co wne a1i adantl syl cvv
      c1 cply cxp cdif wrex caa qcn cid cres cof wss qsscn cz 1z zq ax-mp plyid
      mp2an plyconst mpan caddc qaddcl cmul qmulcl cneg qnegcl ax-1ne0 peano2cn
      wa plysub wfn fnresi fconstg ffn cnex inidm fvresi fvconst2g ofval ax-1cn
      wf mpdan pncan2 sylancl eqtrd 0cn elexi fvconst2 3netr4d necon3i sylanbrc
      fveq1 eldifsn subid eqeq1d rcla4ev syl2anc elqaa ) AEFZAGFZABHZIZJKZBEUAI
      ZGJLUBZLUCZUDZAUEFAUFZWRUGGUHZGALZUBZMUINZXEFZAXKIZJKZXFWRXKXCFXKXDOZXLWR
      CDEXHXJXHXCFZWREGUJZTEFZXPUKTULFXRUMTUNUOZEUPUQPXQWRXJXCFUKAEURUSCHZEFDHZ
      EFVHZXTYAUTNEFWRXTYAVAQYBXTYAVBNEFWRXTYAVCQTVDEFZWRXRYCXSTVEUOPVIWRATUTNZ
      XKIZYDXDIZOXOWRTJYEYFTJOWRVFPWRYEYDAMNZTWRYDGFZYEYGKWRWSYHXGAVGRZWRGGYDAM
      GXHXJSSYDXHGVJWRGVKPZWRGXIXJVTXJGVJGAEVLGXIXJVMRZGSFWRVNPZYLGVOZYHYDXHIYD
      KWRGYDVPQGAYDEVQVRWAWRWSTGFYGTKXGVSATWBWCWDWRYHYFJKYIGJYDJGWEWFWGRWHXKXDY
      EYFYDXKXDWKWIRXKXCXDWLWJWRXMAAMNZJWRWSXMYNKXGWRGGAAMGXHXJSSAYJYKYLYLYMWSA
      XHIAKWRGAVPQGAAEVQVRWAWRWSYNJKXGAWMRWDXBXNBXKXEWTXKKXAXMJAWTXKWKWNWOWPABW
      QWJ $.
      $( [23-Jul-2014] $)

    $( The rational numbers are contained in the algebraic numbers.
       (Contributed by Mario Carneiro, 23-Jul-2014.) $)
    qssaa $p |- QQ C_ AA $=
      ( vx cq caa cv qaa ssriv ) ABCADEF $.
      $( [23-Jul-2014] $)

    $( The imaginary unit is algebraic.  (Contributed by Mario Carneiro,
       23-Jul-2014.) $)
    iaa $p |- _i e. AA $=
      ( vf vz ci wcel cc cv cfv cc0 wceq cz csn c2 cexp co c1 caddc wtru ax-1cn
      a1i ax-mp vx caa cply cxp cdif wrex elaa ax-icn cmpt wne eldifsn cof cnex
      vy cvv sqcl adantl wa eqidd fconstmpt offval2 wss cn0 zsscn 1z 2nn0 mp3an
      plypow plyconst mp2an zaddcl plyadd eqeltrrd trud ax-1ne0 0cn sq0i oveq1d
      fveq1 addid2i syl6eq eqid elexi fvmpt fvconst2 3eqtr3g necon3i cneg oveq1
      mpbir2an i2 negcli addcomi negidi eqtri eqeq1d rcla4ev ) CUBDCEDZCAFZGZHI
      ZAJUCGZEHKUDZKUEZUFZCAUGUHBEBFZLMNZOPNZUIZXDDZCXIGZHIZXEXJXIXBDZXIXCUJZXI
      XBXCUKXMQBEXGUIZEOKUDZPULNXIXBQBEXGOPXOXPUOEEEUODQUMSXFEDZXGEDQXFUPUQOEDQ
      XQURRSQXOUSXPBEOUIIQBEOUTSVAQUAUNJXOXPXOXBDZQJEVBZOJDZLVCDXRVDVEVFBJLVHVG
      SXPXBDZQXSXTYAVDVEOJVIVJSUAFZJDUNFZJDURYBYCPNJDQYBYCVKUQVLVMVNOHUJXNVOXIX
      COHXIXCIHXIGZHXCGZOHHXIXCVSHEDZYDOIVPBHXHOEXIXFHIZXHHOPNOYGXGHOPXFVQVRORV
      TWAXIWBZOERWCWDTYFYEHIVPEHHHEVPWCZWETWFWGTWJWRXLUHBCXHHEXIXFCIZXHOWHZOPNZ
      HYJXGYKOPYJXGCLMNYKXFCLMWIWKWAVRYLOYKPNHYKOORWLRWMORWNWOWAYHYIWDTXAXLAXIX
      DWSXIIWTXKHCWSXIVSWPWQVJWJ $.
      $( [23-Jul-2014] $)

    $(
    @( The algebraic numbers are countable. @)
    aannen @p |- AA ~~ NN @=
      ? @.

    aaliou.1 @e |- N = ( deg ` F ) @.
    aaliou.2 @e |- ( ph -> F e. ( Poly ` ZZ ) ) @.
    aaliou.3 @e |- ( ph -> N e. NN ) @.
    aaliou.4 @e |- ( ph -> A e. CC ) @.
    aaliou.5 @e |- ( ph -> ( F ` A ) = 0 ) @.
    @( Liouville's theorem on diophantine approximation: Any algebraic number,
       being a root of a polynomial ` F ` in integer coefficients, is not
       approximable beyond order ` N = deg ( F ) ` by rational numbers. In this
       form, it also applies to rational numbers themselves, which are not well
       approximable by other rational numbers. @)
    aaliou @p |- ( ph -> E. x e. RR+ A. p e. ZZ A. q e. NN ( A = ( p / q ) \/
      ( x / ( q ^ N ) ) < ( abs ` ( A - ( p / q ) ) ) ) ) @=
      ? @.
    $)
  $}

