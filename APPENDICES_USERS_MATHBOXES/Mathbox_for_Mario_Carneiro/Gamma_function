  $(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             Gamma function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c _G $. $( Gamma function (capital letter Gamma) $)
  $c log_G $. $( logarithm of the Gamma function $)

  $( The Gamma function. $)
  cgam $a class _G $.

  $( Logarithm of the Gamma function. $)
  clgam $a class log_G $.

  ${
    $d z n m $.
    $( Define the log-Gamma function.  We can work with this form of the gamma
       function a bit easier than the equivalent expression for the gamma
       function itself, and moreover this function is not actually equal to
       ` log ( _G ( x ) ) ` because the branch cuts are placed differently (we
       do have ` exp ( log_G ( x ) ) = _G ( x ) ` , though).  This definition
       is attributed to Euler, and unlike the usual integral definition is
       defined on the entire complex plane except the nonpositive integers
       ` ZZ \ NN ` , where the function has simple poles. $)
    df-lgam $a |- log_G = ( z e. ( CC \ ( ZZ \ NN ) ) |-> ( ~~> ` ( n e. NN |->
      ( ( ( z x. ( log ` n ) ) - ( log ` z ) ) -
        sum_ m e. ( 1 ... n ) ( log ` ( ( z / m ) + 1 ) ) ) ) ) ) $.

    $( Define the Gamma function.  See ~ df-lgam for more information about the
       reason for this definition in terms of the log-gamma function. $)
    df-gam $a |- _G = ( exp o. log_G ) $.
  $}

  $( Elementhood in the set of non-nonpositive integers. $)
  eldmgm $p |- ( A e. ( CC \ ( ZZ \ NN ) ) <->
                ( A e. CC /\ -. -u A e. NN0 ) ) $=
    ( cc cz cn cdif wcel wn wa cneg cn0 eldif cr wo elznn cc0 wbr clt syl mpbid
    wb simprbi ord imp wceq negneg adantr nn0negz adantl eqeltrrd ex cle nn0ge0
    wi nngt0 nnre lt0neg2 renegcl 0re ltnle sylancl con2i impbid2 syl5bb notbid
    a1i jcad pm5.32i bitri ) ABCDEZEFABFZAVIFZGZHVJAIZJFZGZHABVIKVJVLVOVJVKVNVK
    ACFZADFZGZHZVJVNACDKVJVSVNVPVRVNVPVQVNVPALFZVQVNMANUAUBUCVJVNVPVRVJVNVPVJVN
    HVMIZACVJWAAUDVNAUEUFVNWACFVJVMUGUHUIUJVNVRUMVJVNOVMUKPZVRVMULVQWBVQVMOQPZW
    BGZVQOAQPZWCAUNVQVTWEWCTAUOZAUPRSVQVMLFZOLFWCWDTVQVTWGWFAUQRURVMOUSUTSVARVE
    VFVBVCVDVGVH $.
    $( [12-Jul-2014] $)

  $( If ` A ` is not a nonpositive integer, then ` A + N ` is nonzero for any
     nonnegative integer ` N ` . $)
  dmgmaddn0 $p |- ( ( A e. ( CC \ ( ZZ \ NN ) ) /\ N e. NN0 ) ->
                    ( A + N ) =/= 0 ) $=
    ( cc cz cn cdif wcel cn0 wa cneg wn caddc co cc0 eldmgm simprbi adantr wceq
    wne cmin df-neg eqeq1i wb 0cn a1i eldifi nn0cn adantl subadd syl3anc syl5bb
    simpr eleq1 syl5ibrcom sylbird necon3bd mpd ) ACDEFZFGZBHGZIZAJZHGZKZABLMZN
    SUSVDUTUSACGZVDAOPQVAVCVENVAVENRZVBBRZVCVHNATMZBRZVAVGVBVIBAUAUBVANCGZVFBCG
    ZVJVGUCVKVAUDUEUSVFUTACURUFQUTVLUSBUGUHNABUIUJUKVAVCVHUTUSUTULVBBHUMUNUOUPU
    Q $.
    $( [12-Jul-2014] $)

  $(
  @( Taylor series of the logarithm function. @)
  logtlr @p |- ( ( A e. CC /\ ( abs ` A ) < 1 /\ N e. NN0 ) ->
        ( abs ` ( ( log ` ( 1 + A ) ) - sum_ k e. ( 1 ... N )
          ( ( ( -u 1 ^ ( k + 1 ) ) / k ) x. ( A ^ k ) ) ) ) <_
      ( ( ( abs ` A ) ^ ( N + 1 ) ) / ( 1 - ( abs ` A ) ) ) ) @=
    ? @.
  $)

  ${
    $d m x y A $.  $d x y N $.
    gamcvg.1 $e |- S = seq 0 ( x. , ( m e. _V |-> ( A + m ) ) ) $.
    $( If ` A ` is not a nonpositive integer, then
       ` prod_ m e. ( 1 ... N ) A + m ` is nonzero for any ` N ` . $)
    dmgmseqn0 $p |- ( ( A e. ( CC \ ( ZZ \ NN ) ) /\ N e. NN0 ) ->
                      ( S ` N ) =/= 0 ) $=
      ( vx cc cdif wcel cn0 wa cfv cc0 wne cmul cvv cv caddc co eldifsn vy cmpt
      cz csn cseq fveq1i cuz simpr nn0uz syl6eleq cfz wceq vex oveq2 eqid fvmpt
      cn ovex ax-mp eldifi adantr elfznn0 nn0cn syl addcl syl2an simpl sylanbrc
      dmgmaddn0 syl5eqel mulcl ad2ant2r mulne0 syl2anb adantl seqcl simprbi ) A
      GUCUQHZHIZDJIZKZDBLZGMUDHZIZWBMNZWAWBDOCPACQZRSZUBZMUEZLWCDBWIEUFWAFUAOWC
      WHMDWADJMUGLVSVTUHUIUJWAFQZMDUKSIZKZWJWHLZAWJRSZWCWJPIWMWNULFUMCWJWGWNPWH
      WFWJARUNWHUOAWJRURUPUSWLWNGIZWNMNZWNWCIWAAGIZWJGIZWOWKVSWQVTAGVRUTVAWKWJJ
      IZWRWJDVBZWJVCVDAWJVEVFWAVSWSWPWKVSVTVGWTAWJVIVFWNGMTVHVJWJWCIZUAQZWCIZKW
      JXBOSZWCIZWAXAWRWJMNZKZXBGIZXBMNZKZXEXCWJGMTXBGMTXGXJKXDGIZXDMNXEWRXHXKXF
      XIWJXBVKVLWJXBVMXDGMTVHVNVOVPVJWDWBGIWEWBGMTVQVD $.
      $( [12-Jul-2014] $)

    $(
    gamcvg.2 @e |- F = ( n e. NN |->
      ( ( ( n ^c A ) x. ( ! ` n ) ) / ( S ` n ) ) ) @.
    gamcvg.3 @e |- G = ( m e. ( ZZ>= ` 2 ) |->
      ( ( F ` m ) - ( F ` ( m - 1 ) ) ) ) @.
    @( The sequence ` F ` is convergent, and its convergent point is
       ` _G ( A ) ` by definition. @)
    gamcvg @p |- ( A e. ( CC \ ( ZZ \ NN ) ) -> F ~~> ( _G ` A ) ) @=
      ? @.
    $)
  $}

