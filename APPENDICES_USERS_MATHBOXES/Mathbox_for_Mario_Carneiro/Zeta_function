$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             Zeta function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c zeta $. $( Riemann zeta function $)

  $( The Riemann zeta function. $)
  czeta $a class zeta $.

  ${
    $d s n k $.
    $( Define the Riemann zeta function. $)
    df-zeta $a |- zeta = ( s e. ( CC \ { 1 } ) |-> ( sum_ n e. NN0
        ( sum_ k e. ( 0 ... n ) ( ( ( -u 1 ^ k ) x. ( n _C k ) ) x.
            ( exp ` ( ( log ` ( k + 1 ) ) x. s ) ) ) / ( 2 ^ ( n + 1 ) ) )
        / ( 1 - ( exp ` ( ( log ` 2 ) x. ( 1 - s ) ) ) ) ) ) $.
  $}

  ${
    $d k n s S $.
    $( The value of the zeta function. $)
    zetaval $p |- ( ( S e. CC /\ S =/= 1 ) -> ( zeta ` S ) =
      ( sum_ n e. NN0
        ( sum_ k e. ( 0 ... n ) ( ( ( -u 1 ^ k ) x. ( n _C k ) ) x.
            ( exp ` ( ( log ` ( k + 1 ) ) x. S ) ) ) / ( 2 ^ ( n + 1 ) ) )
        / ( 1 - ( exp ` ( ( log ` 2 ) x. ( 1 - S ) ) ) ) ) ) $=
      ( vs cc wcel c1 czeta cfv cn0 cv co cexp cmul caddc csu cdiv cmin oveq2d
      ce wne wa csn cdif cc0 cfz cneg cbc clog c2 wceq eldifsn fveq2d sumeq2sdv
      oveq2 oveq1d oveq12d df-zeta ovex fvmpt sylbir ) AEFAGUAUBAEGUCUDZFAHIJUE
      CKZUFLZGUGBKZMLVCVEUHLNLZVEGOLUIIZANLZTIZNLZBPZUJVCGOLMLZQLZCPZGUJUIIZGAR
      LZNLZTIZRLZQLZUKAEGULDAJVDVFVGDKZNLZTIZNLZBPZVLQLZCPZGVOGWARLZNLZTIZRLZQL
      VTVBHWAAUKZWGVNWKVSQWLJWFVMCWLWEVKVLQWLVDWDVJBWLWCVIVFNWLWBVHTWAAVGNUOUMS
      UNUPUNWLWJVRGRWLWIVQTWLWHVPVONWAAGRUOSUMSUQBCDURVNVSQUSUTVA $.
      $( [22-Dec-2013] $)
  $}

  $(
  @( Derive the standard expression for the zeta function. (This is usually
     quoted as the definition of the zeta function, but it is not convergent
     everywhere, unlike our definition.) @)
  zetaval2 @p |- ( ( S e. CC /\ 1 < ( Re ` S ) ) -> ( zeta ` S ) =
    sum_ n e. NN ( exp ` ( ( log ` n ) x. -u S ) ) ) @=
    ? @.

  @{
    @( The laurent expansion of the zeta function about the pole at ` s = 1 ` .
       The function ` J ( n ) ` is commonly known as the ` n ` -th Stieltjes
       constant. @)
    zetaval3.1 @e |- J = ( ~~> ` ( m e. NN |->
          ( sum_ k e. ( 1 ... m ) ( ( ( log ` k ) ^ n ) / k ) -
            ( ( ( log ` m ) ^ ( n + 1 ) ) / ( n + 1 ) ) ) ) ) @.
    zetaval3 @p |- ( ( S e. CC /\ S =/= 1 ) -> ( zeta ` S ) =
      ( ( 1 / ( S - 1 ) ) + sum_ n e. NN0
        ( ( ( ( 1 - S ) ^ n ) / ( ! ` n ) ) x. J ) ) ) @=
      ? @.
  @}

  @( The value of the zeta function on the natural numbers. @)
  zetann @p |- ( S e. ( ZZ>= ` 2 ) ->
    ( zeta ` S ) = sum_ n e. NN ( 1 / ( n ^ S ) ) ) @=
    ? @.

  @( The value of ` zeta ( 0 ) ` . @)
  zeta0 @p |- ( zeta ` 0 ) = -u ( 1 / 2 ) @=
    ? @.

  @( The value of ` zeta ( 2 ) ` , whose derivation (with an imprecise proof)
     is attributed to Euler. @)
  zeta2 @p |- ( zeta ` 2 ) = ( ( pi ^ 2 ) / 6 ) @=
    ? @.

  @( Apery's constant, ` zeta ( 3 ) ` , is irrational. @)
  zeta3irr @p |- ( zeta ` 3 ) e/ QQ @=
    ? @.

  @( The trivial zeroes of the zeta function. @)
  zetatrivz @p |- ( N e. NN -> ( zeta ` ( -u 2 x. N ) ) = 0 ) @=
    ? @.

  @( The nontrivial zeroes of the zeta function are in the critical strip.
     The Riemann Hypothesis would replace this conclusion with
     ` ( Re `` S ) = ( 1 / 2 ) ` . @)
  zetazcs @p |- ( ( S e. ( CC \ ZZ ) /\ ( zeta ` S ) = 0 ) ->
      ( Re ` S ) e. ( 0 (,) 1 ) ) @=
    ? @.
  $)

