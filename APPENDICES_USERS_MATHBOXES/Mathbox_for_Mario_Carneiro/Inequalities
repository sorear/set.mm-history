$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             Inequalities
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c A $.  $d b c B $.  $d c C $.  $d a b c F $.  $d a b c S $.
    pmltpclem1.1 $e |- ( ph -> A e. S ) $.
    pmltpclem1.2 $e |- ( ph -> B e. S ) $.
    pmltpclem1.3 $e |- ( ph -> C e. S ) $.
    pmltpclem1.4 $e |- ( ph -> A < B ) $.
    pmltpclem1.5 $e |- ( ph -> B < C ) $.
    pmltpclem1.6 $e |- ( ph ->
      ( ( ( F ` A ) < ( F ` B ) /\ ( F ` C ) < ( F ` B ) ) \/
        ( ( F ` B ) < ( F ` A ) /\ ( F ` B ) < ( F ` C ) ) ) ) $.
    $( Lemma for ~ pmltpc . $)
    pmltpclem1 $p |- ( ph ->
      E. a e. S E. b e. S E. c e. S ( a < b /\ b < c /\
        ( ( ( F ` a ) < ( F ` b ) /\ ( F ` c ) < ( F ` b ) ) \/
          ( ( F ` b ) < ( F ` a ) /\ ( F ` b ) < ( F ` c ) ) ) ) ) $=
      ( clt wbr cfv wa wo wcel w3a wrex wceq breq1 breq1d anbi1d breq2d orbi12d
      fveq2 3anbi13d breq2 anbi12d 3anbi123d anbi2d 3anbi23d rcla43ev syl33anc
      cv ) ABEUACEUADEUABCPQZCDPQZBFRZCFRZPQZDFRZVCPQZSZVCVBPQZVCVEPQZSZTZGUSZH
      USZPQZVMIUSZPQZVLFRZVMFRZPQZVOFRZVRPQZSZVRVQPQZVRVTPQZSZTZUBZIEUCHEUCGEUC
      JKLMNOWGUTVAVKUBBVMPQZVPVBVRPQZWASZVRVBPQZWDSZTZUBUTCVOPQZVDVTVCPQZSZVHVC
      VTPQZSZTZUBGHIBCDEEEVLBUDZVNWHWFWMVPVLBVMPUEWTWBWJWEWLWTVSWIWAWTVQVBVRPVL
      BFUJZUFUGWTWCWKWDWTVQVBVRPXAUHUGUIUKVMCUDZWHUTVPWNWMWSVMCBPULVMCVOPUEXBWJ
      WPWLWRXBWIVDWAWOXBVRVCVBPVMCFUJZUHXBVRVCVTPXCUHUMXBWKVHWDWQXBVRVCVBPXCUFX
      BVRVCVTPXCUFUMUIUNVODUDZWNVAWSVKUTVODCPULXDWPVGWRVJXDWOVFVDXDVTVEVCPVODFU
      JZUFUOXDWQVIVHXDVTVEVCPXEUHUOUIUPUQUR $.
      $( [1-Jul-2014] $)
  $}

  ${
    $d a b c w x y z A $.  $d a b c w x y z F $.  $d w x y z ph $.  $d b c V $.
    $d a b c U $.  $d a b c W $.  $d b c X $.
    pmltpc.1 $e |- ( ph -> F e. ( RR ^pm RR ) ) $.
    pmltpc.2 $e |- ( ph -> A C_ dom F ) $.
    pmltpc.3 $e |- ( ph -> U e. A ) $.
    pmltpc.4 $e |- ( ph -> V e. A ) $.
    pmltpc.5 $e |- ( ph -> W e. A ) $.
    pmltpc.6 $e |- ( ph -> X e. A ) $.
    pmltpc.7 $e |- ( ph -> U <_ V ) $.
    pmltpc.8 $e |- ( ph -> W <_ X ) $.
    pmltpc.9 $e |- ( ph -> -. ( F ` U ) <_ ( F ` V ) ) $.
    pmltpc.10 $e |- ( ph -> -. ( F ` X ) <_ ( F ` W ) ) $.
    $( Lemma for ~ pmltpc . $)
    pmltpclem2 $p |- ( ph -> E. a e. A E. b e. A E. c e. A ( a < b /\ b < c /\
      ( ( ( F ` a ) < ( F ` b ) /\ ( F ` c ) < ( F ` b ) ) \/
        ( ( F ` b ) < ( F ` a ) /\ ( F ` b ) < ( F ` c ) ) ) ) ) $=
      ( cv clt wbr cfv wa wo w3a wrex wcel ad2antrr simpr wne cr cdm wf wss cpm
      co reex elpm2 sylib simpld sseldd ffvelrn syl2anc wn wb ltnle mpbird ltne
      cle syl3anc wceq fveq2 eqcomd necon3i syl simprd leltne simplr pmltpclem1
      jca orcd olcd adantr pm2.61ltlei lelttrd lttrd ) AHUAZIUAZUBUCWJJUAZUBUCW
      IDUDZWJDUDZUBUCWKDUDZWMUBUCUEWMWLUBUCWMWNUBUCUEUFUGJBUHIBUHHBUHZFDUDZCDUD
      ZAWPWQUBUCZUEZWOFCWSFCUBUCZUEZFCEBDHIJAFBUIZWRWTOUJACBUIZWRWTMUJAEBUIZWRW
      TNUJWSWTUKACEUBUCZWRWTAXEECULZAWQEDUDZULZXFAXGUMUIZWQUMUIZXGWQUBUCZXHADUN
      ZUMDUOZEXLUIXIAXMXLUMUPZADUMUMUQURUIXMXNUEKUMUMDUSUSUTVAZVBZABXLELNVCZXLU
      MEDVDVEZAXMCXLUIXJXPABXLCLMVCZXLUMCDVDVEZAXKWQXGVKUCVFZSAXIXJXKYAVGXRXTXG
      WQVHVEVIZXGWQVJVLECWQXGECVMXGWQECDVNVOVPVQACUMUIZEUMUIZCEVKUCXEXFVGAXLUMC
      AXMXNXOVRZXSVCZAXLUMEYEXQVCZQCEVSVLVIZUJXAWRXKUEWQWPUBUCZWQXGUBUCZUEXAWRX
      KAWRWTVTAXKWRWTYBUJWBWCWAWSCFVKUCZUEZCFGBDHIJAXCWRYKMUJAXBWRYKOUJAGBUIZWR
      YKPUJYLCFUBUCZFCULZYLWQWPULZYOYLWPUMUIZXJWRYPAYQWRYKAXMFXLUIYQXPABXLFLOVC
      ZXLUMFDVDVEZUJAXJWRYKXTUJAWRYKVTZWPWQVJVLFCWQWPFCVMWPWQFCDVNVOVPVQYLYCFUM
      UIZYKYNYOVGAYCWRYKYFUJAUUAWRYKAXLUMFYEYRVCZUJWSYKUKCFVSVLVIAFGUBUCZWRYKAU
      UCGFULZAGDUDZWPULZUUDAYQUUEUMUIZWPUUEUBUCZUUFYSAXMGXLUIUUGXPABXLGLPVCZXLU
      MGDVDVEZAUUHUUEWPVKUCVFZTAYQUUGUUHUUKVGYSUUJWPUUEVHVEVIZWPUUEVJVLGFUUEWPG
      FDVNVPVQAUUAGUMUIZFGVKUCUUCUUDVGUUBAXLUMGYEUUIVCZRFGVSVLVIZUJYLWRUUHUEYIU
      UEWPUBUCZUEYLWRUUHYTAUUHWRYKUULUJWBWDWAAUUAWRUUBWEAYCWRYFWEWFAWQWPVKUCZUE
      ZWOEGUUREGUBUCZUEZCEGBDHIJAXCUUQUUSMUJAXDUUQUUSNUJAYMUUQUUSPUJAXEUUQUUSYH
      UJUURUUSUKUUTXKXGUUEUBUCZUEYJUUEXGUBUCZUEUUTXKUVAAXKUUQUUSYBUJUURUVAUUSUU
      RXGWQUUEAXIUUQXRWEZAXJUUQXTWEZAUUGUUQUUJWEZAXKUUQYBWEUURWQWPUUEUVDAYQUUQY
      SWEUVEAUUQUKAUUHUUQUULWEWGWHZWEWBWDWAUURGEVKUCZUEZFGEBDHIJAXBUUQUVGOUJAYM
      UUQUVGPUJAXDUUQUVGNUJAUUCUUQUVGUUOUJUVHGEUBUCZEGULZUVHUUEXGULZUVJUVHXIUUG
      UVAUVKUURXIUVGUVCWEUURUUGUVGUVEWEUURUVAUVGUVFWEZXGUUEVJVLEGUUEXGEGVMXGUUE
      EGDVNVOVPVQUVHUUMYDUVGUVIUVJVGAUUMUUQUVGUUNUJAYDUUQUVGYGUJUURUVGUKGEVSVLV
      IUVHUUHUVAUEUUPUVBUEUVHUUHUVAAUUHUUQUVGUULUJUVLWBWCWAAYDUUQYGWEAUUMUUQUUN
      WEWFYSXTWF $.
      $( [1-Jul-2014] $)
  $}

  ${
    $d a b c w x y z A $.  $d a b c w x y z F $.
    $( Any function on the reals is either increasing, decreasing, or has a
       triple of points in a vee formation.  (This theorem was created on
       demand by Mario Carneiro for the 6PCM conference in Bialystok,
       1-Jul-2014.) $)
    pmltpc $p |- ( ( F e. ( RR ^pm RR ) /\ A C_ dom F ) ->
      ( A. x e. A A. y e. A ( x <_ y -> ( F ` x ) <_ ( F ` y ) ) \/
        A. x e. A A. y e. A ( x <_ y -> ( F ` y ) <_ ( F ` x ) ) \/
        E. a e. A E. b e. A E. c e. A ( a < b /\ b < c /\
        ( ( ( F ` a ) < ( F ` b ) /\ ( F ` c ) < ( F ` b ) ) \/
          ( ( F ` b ) < ( F ` a ) /\ ( F ` b ) < ( F ` c ) ) ) ) ) ) $=
      ( vz vw wcel wa cv cle wbr cfv wi wral clt wrex wn cr cpm cdm wss w3a w3o
      co wo rexanali rexbii rexnal bitri wceq breq1 fveq2 breq2d imbi12d breq1d
      breq2 cbvral2v notbii 3bitri anbi12i reeanv 3bitr4i simplll simpld simprd
      simpllr simplrl simplrr simprll simprrl simprlr simprrr pmltpclem2 df-3or
      ioran ex rexlimdvva syl5bir orrd sylibr ) DUAUAUBUGJZCDUCUDZKZALZBLZMNZWG
      DOZWHDOZMNZPBCQZACQZWIWKWJMNZPZBCQACQZUHZELZFLZRNWTGLZRNWSDOZWTDOZRNXADOZ
      XCRNKXCXBRNXCXDRNKUHUEGCSFCSECSZUHWNWQXEUFWFWRXEWRTZWIWLTZKZBCSZHLZILZMNZ
      XKDOZXJDOZMNZTZKZICSZKZHCSACSZWFXEXIACSZXRHCSZKWNTZWQTZKXTXFYAYCYBYDYAWMT
      ZACSYCXIYEACWIWLBCUIUJWMACUKULYBXLXOPZICQZTZHCSYGHCQZTYDXRYHHCXLXOICUIUJY
      GHCUKYIWQYFWPWGXKMNZXMWJMNZPHIABCCXJWGUMZXLYJXOYKXJWGXKMUNYLXNWJXMMXJWGDU
      OUPUQXKWHUMZYJWIYKWOXKWHWGMUSYMXMWKWJMXKWHDUOURUQUTVAVBVCXIXRAHCCVDWNWQVR
      VEWFXSXEAHCCXSXHXQKZICSBCSWFWGCJZXJCJZKZKZXEXHXQBICCVDYRYNXEBICCYRWHCJZXK
      CJZKZKZYNXEUUBYNKZCWGDWHXJXKEFGUUCWDWEWFYQUUAYNVFZVGUUCWDWEUUDVHUUCYOYPWF
      YQUUAYNVIZVGYRYSYTYNVJUUCYOYPUUEVHYRYSYTYNVKUUBWIXGXQVLUUBXHXLXPVMUUBWIXG
      XQVNUUBXHXLXPVOVPVSVTWAVTWAWBWNWQXEVQWC $.
      $( [1-Jul-2014] $)
  $}

  $( Arithmetic-geometric mean inequality for ` n = 2 ` . $)
  amgm2 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
      ( sqr ` ( A x. B ) ) <_ ( ( A + B ) / 2 ) ) $=
    ( cr wcel cc0 cle wbr wa c2 cmul co caddc cexp c4 cc wceq syl2anc recnd syl
    cmin csqr cfv 2cn simpll simprl remulcl mulge0 resqrcl sqmul sylancr oveq1i
    sq2 sqrth oveq2d syl5eq eqtrd resubcl sqge0 binom2 binom2sub oveq12d resqcl
    cdiv 2re readdcl pnpcan2 syl3anc pnncan mulass syl5eqr 2times eqtr4d 3eqtrd
    2t2e4 a1i 4re subsub23 mpbid breqtrrd subge0 eqbrtrd sqrge0 0re 2pos ltleii
    wb mpanl12 addge0 an4s le2sq syl22anc mpbird clt pm3.2i lemuldiv2 ) ACDZEAF
    GZHZBCDZEBFGZHZHZIABJKZUAUBZJKZABLKZFGZXDXFIVCKFGZXBXGXEIMKZXFIMKZFGZXBXINX
    CJKZXJFXBXIIIMKZXDIMKZJKZXLXBIODZXDODXIXOPUCXBXDXBXCCDZEXCFGZXDCDZXBWPWSXQW
    PWQXAUDZWRWSWTUEZABUFQZABUGZXCUHQZRIXDUIUJXBXONXNJKXLXMNXNJULUKXBXNXCNJXBXC
    ODZXNXCPXBXCYBRZXCUMSUNUOUPXBEXJXLTKZFGZXLXJFGZXBEABTKZIMKZYGFXBYJCDZEYKFGX
    BWPWSYLXTYAABUQQZYJURSXBXJYKTKZXLPZYGYKPZXBYNAIMKZIXCJKZLKZBIMKZLKZYQYRTKZY
    TLKZTKZYSUUBTKZXLXBXJUUAYKUUCTXBAODZBODZXJUUAPXBAXTRZXBBYARZABUSQXBUUFUUGYK
    UUCPUUHUUIABUTQVAXBYSODUUBODYTODUUDUUEPXBYSXBYQCDZYRCDZYSCDXBWPUUJXTAVBSZXB
    ICDZXQUUKVDYBIXCUFUJZYQYRVEQRXBUUBXBUUJUUKUUBCDUULUUNYQYRUQQRXBYTXBWSYTCDYA
    BVBSRYSUUBYTVFVGXBUUEYRYRLKZXLXBYQODYRODZUUPUUEUUOPXBYQUULRXBYRUUNRZUUQYQYR
    YRVHVGXBXLIYRJKZUUOXBXLIIJKZXCJKZUURUUSNXCJVNUKXBXPXPYEUUTUURPXPXBUCVOZUVAY
    FIIXCVIVGVJXBUUPUURUUOPUUQYRVKSUPVLVMXBXJODYKODXLODYOYPWFXBXJXBXFCDZXJCDZXB
    WPWSUVBXTYAABVEQZXFVBSZRXBYKXBYLYKCDYMYJVBSRXBXLXBNCDXQXLCDZVPYBNXCUFUJZRXJ
    YKXLVQVGVRVSXBUVCUVFYHYIWFUVEUVGXJXLVTQVRWAXBXECDZEXEFGZUVBEXFFGZXGXKWFXBUU
    MXSUVHVDYDIXDUFUJXBXSEXDFGZUVIYDXBXQXRUVKYBYCXCWBQUUMEIFGXSUVKHUVIVDEIWCVDW
    DWEIXDUGWGQUVDWPWSWQWTUVJABWHWIXEXFWJWKWLXBXSUVBUUMEIWMGZHZXGXHWFYDUVDUVMXB
    UUMUVLVDWDWNVOXDXFIWOVGVR $.
    $( [2-Jul-2014] $)

