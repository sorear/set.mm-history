  $(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             Function operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c oF $.

  $( Extend class notation to include mapping of an operation to a function
     operation. $)
  cof $a class oF F $.

  ${
    $d f g x R $.
    $( Define the function operation map.  The definition is designed so that
       if ` R ` is a binary operation, then ` oF R ` is the analogous operation
       on functions which corresponds to applying ` R ` pointwise to the values
       of the functions. $)
    df-of $a |- oF R = ( f e. _V , g e. _V |->
     ( x e. ( dom f i^i dom g ) |-> ( ( f ` x ) R ( g ` x ) ) ) ) $.
  $}

  ${
    $d f g x z R $.  $d f g x y z S $.
    $( Equality theorem for function operation.  (Contributed by Mario
       Carneiro, 20-Jul-2014.) $)
    ofeq $p |- ( R = S -> oF R = oF S ) $=
      ( vf vg vx wceq cvv cv cdm cin cfv co cmpt cmpt2 cof wcel wa simpl1 df-of
      w3a oveqd mpteq2dva mpt2eq3dva 3eqtr4g ) ABFZCDGGECHZIDHZIJZEHZUFKZUIUGKZ
      ALZMZNCDGGEUHUJUKBLZMZNAOBOUECDGGUMUOUEUFGPZUGGPZTZEUHULUNURUIUHPZQABUJUK
      UEUPUQUSRUAUBUCEACDSEBCDSUD $.
      $( [20-Jul-2014] $)

    hbof.1 $e |- ( x e. R -> A. y x e. R ) $.
    $( Hypothesis builder for function operation.  (Contributed by Mario
       Carneiro, 20-Jul-2014.) $)
    hbof $p |- ( x e. oF R -> A. y x e. oF R ) $=
      ( vf vg vz cof cvv cv cdm cin cfv co cmpt cmpt2 df-of wcel ax-17 hbov
      hbmpt hbmpt2 hbxfreq ) BACHEFIIGEJZKFJZKLZGJZUDMZUGUEMZCNZOZPGCEFQEFBAIIU
      KAJZIRBSZUMBGAUFUJULUFRBSBAUHUICULUHRBSDULUIRBSTUAUBUC $.
      $( [20-Jul-2014] $)
  $}

  ${
    $d x A $.  $d f g x F $.  $d f g x G $.  $d x ph $.  $d x S $.  $d x X $.
    $d f g x R $.
    offval.1 $e |- ( ph -> F Fn A ) $.
    offval.2 $e |- ( ph -> G Fn B ) $.
    offval.3 $e |- ( ph -> A e. V ) $.
    offval.4 $e |- ( ph -> B e. W ) $.
    offval.5 $e |- ( A i^i B ) = S $.
    ${
      offval.6 $e |- ( ( ph /\ x e. A ) -> ( F ` x ) = C ) $.
      offval.7 $e |- ( ( ph /\ x e. B ) -> ( G ` x ) = D ) $.
      $( Value of an operation applied to two functions.  (Contributed by Mario
         Carneiro, 20-Jul-2014.) $)
      offval $p |- ( ph -> ( F oF R G ) = ( x e. S |-> ( C R D ) ) ) $=
        ( wceq vf vg cof co cdm cin cfv cmpt cvv wcel wfn fnex syl2anc fndm syl
        cv ineq12d syl6eq mpteq1 inex1g syl5eqelr mptexg 3syl eqeltrd wa ineq12
        dmeq syl2an fveq1 oveq12 mpteq12dv df-of ovmpt2ga syl3anc eleq2i bitr3i
        elin adantrr adantrl oveq12d sylan2b mpteq2dva 3eqtrd ) AIJGUCZUDZBIUEZ
        JUEZUFZBUPZIUGZWIJUGZGUDZUHZBHWLUHZBHEFGUDZUHAIUIUJZJUIUJZWMUIUJWEWMTAI
        CUKZCKUJZWPMOCKIULUMAJDUKZDLUJWQNPDLJULUMAWMWNUIAWHHTWMWNTAWHCDUFZHAWFC
        WGDAWRWFCTMCIUNUOAWTWGDTNDJUNUOUQQURBWHHWLUSUOZAWSHUIUJWNUIUJOWSHXAUIQC
        DKUTVABHWLUIVBVCVDUAUBIJUIUIBUAUPZUEZUBUPZUEZUFZWIXCUGZWIXEUGZGUDZUHWMW
        DUIXCITZXEJTZVEBXGXJWHWLXKXDWFTXFWGTXGWHTXLXCIVGXEJVGXDWFXFWGVFVHXKXHWJ
        TXIWKTXJWLTXLWIXCIVIWIXEJVIXHWJXIWKGVJVHVKBGUAUBVLVMVNXBABHWLWOWIHUJZAW
        ICUJZWIDUJZVEZWLWOTXMWIXAUJXPXAHWIQVOWICDVQVPAXPVEWJEWKFGAXNWJETXORVRAX
        OWKFTXNSVSVTWAWBWC $.
        $( [20-Jul-2014] $)
    $}

    ${
      ofval.6 $e |- ( ( ph /\ X e. A ) -> ( F ` X ) = C ) $.
      ofval.7 $e |- ( ( ph /\ X e. B ) -> ( G ` X ) = D ) $.
      $( Evaluate a function operation at a point.  (Contributed by Mario
         Carneiro, 20-Jul-2014.) $)
      ofval $p |- ( ( ph /\ X e. S ) -> ( ( F oF R G ) ` X ) = ( C R D ) ) $=
        ( cfv vx wcel wa cof co cv cmpt wceq eqidd offval fveq1d adantr oveq12d
        fveq2 eqid ovex fvmpt adantl inss1 eqsstr3i sseli sylan2 inss2 3eqtrd
        cin ) ALGUBZUCZLHIFUDUEZTZLUAGUAUFZHTZVJITZFUEZUGZTZLHTZLITZFUEZDEFUEAV
        IVOUHVFALVHVNAUABCVKVLFGHIJKMNOPQAVJBUBUCVKUIAVJCUBUCVLUIUJUKULVFVOVRUH
        AUALVMVRGVNVJLUHVKVPVLVQFVJLHUNVJLIUNUMVNUOVPVQFUPUQURVGVPDVQEFVFALBUBV
        PDUHGBLGBCVEZBQBCUSUTVARVBVFALCUBVQEUHGCLGVSCQBCVCUTVASVBUMVD $.
        $( [20-Jul-2014] $)
    $}

    $( The function operation produces a function.  (Contributed by Mario
       Carneiro, 22-Jul-2014.) $)
    offn $p |- ( ph -> ( F oF R G ) Fn S ) $=
      ( vx co wfn cfv cvv wcel cof cv cmpt eqid fnmpt ovex mprg wa eqidd offval
      a1i fneq1d mpbiri ) AFGDUAPZEQOEOUBZFRZUOGRZDPZUCZEQZURSTZUTOEOEURUSSUSUD
      UEVAUOETUPUQDUFUKUGAEUNUSAOBCUPUQDEFGHIJKLMNAUOBTUHUPUIAUOCTUHUQUIUJULUM
      $.
      $( [22-Jul-2014] $)
  $}

  ${
    $d z A $.  $d z C $.  $d y z G $.  $d x y z ph $.  $d x y S $.  $d x y T $.
    $d x y z F $.  $d x y z R $.  $d x y z U $.
    off.1 $e |- ( ( ph /\ ( x e. S /\ y e. T ) ) -> ( x R y ) e. U ) $.
    off.2 $e |- ( ph -> F : A --> S ) $.
    off.3 $e |- ( ph -> G : B --> T ) $.
    off.4 $e |- ( ph -> A e. V ) $.
    off.5 $e |- ( ph -> B e. W ) $.
    off.6 $e |- ( A i^i B ) = C $.
    $( The function operation produces a function.  (Contributed by Mario
       Carneiro, 20-Jul-2014.) $)
    off $p |- ( ph -> ( F oF R G ) : C --> U ) $=
      ( vz cof co wf cv cfv cmpt wcel wa wral cin inss1 eqsstr3i ffvelrn syl2an
      sseli inss2 ralrimivva adantr wceq oveq1 eleq1d oveq2 rcla42va eqid fmptd
      syl21anc wfn ffn syl eqidd offval feq1d mpbird ) AFJKLGUBUCZUDFJUAFUAUEZK
      UFZVPLUFZGUCZUGZUDAUAFVSJVTAVPFUHZUIVQHUHZVRIUHZBUEZCUEZGUCZJUHZCIUJBHUJZ
      VSJUHZADHKUDZVPDUHZWBWAPFDVPFDEUKZDTDEULUMUPDHVPKUNUOAEILUDZVPEUHZWCWAQFE
      VPFWLETDEUQUMUPEIVPLUNUOAWHWAAWGBCHIOURUSWGWIVQWEGUCZJUHBCVQVRHIWDVQUTWFW
      OJWDVQWEGVAVBWEVRUTWOVSJWEVRVQGVCVBVDVGVTVEVFAFJVOVTAUADEVQVRGFKLMNAWJKDV
      HPDHKVIVJAWMLEVHQEILVIVJRSTAWKUIVQVKAWNUIVRVKVLVMVN $.
      $( [20-Jul-2014] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d y z C $.  $d y F $.  $d y G $.  $d x y ph $.
    $d x y z R $.  $d x W $.  $d x X $.
    offval2.1 $e |- ( ph -> A e. V ) $.
    offval2.2 $e |- ( ( ph /\ x e. A ) -> B e. W ) $.
    offval2.3 $e |- ( ( ph /\ x e. A ) -> C e. X ) $.
    offval2.4 $e |- ( ph -> F = ( x e. A |-> B ) ) $.
    offval2.5 $e |- ( ph -> G = ( x e. A |-> C ) ) $.
    $( The function operation expressed as a mapping.  (Contributed by Mario
       Carneiro, 20-Jul-2014.) $)
    offval2 $p |- ( ph -> ( F oF R G ) = ( x e. A |-> ( B R C ) ) ) $=
      ( vy vz cmpt wcel cof co cv cfv wfn eqid fmptd feq1d mpbird ffn syl inidm
      wf wa wceq adantr fveq1d offval hbmpt1 ax-17 hbfv hbov fveq2 cbvmpt simpr
      oveq12d fvmpt2 syl2anc mpteq2dva syl5eq eqtrd ) AGHFUAUBQCQUCZBCDSZUDZVLB
      CESZUDZFUBZSZBCDEFUBZSZAQCCVNVPFCGHIIACJGUMZGCUEAWACJVMUMABCDJVMMVMUFZUGA
      CJGVMOUHUICJGUJUKACKHUMZHCUEAWCCKVOUMABCEKVONVOUFZUGACKHVOPUHUICKHUJUKLLC
      ULAVLCTZUNZVLGVMAGVMUOWEOUPUQWFVLHVOAHVOUOWEPUPUQURAVRBCBUCZVMUDZWGVOUDZF
      UBZSVTQBRCVQWJBRVNVPFBRVLVMBRCDUSRUCZVLTBUTZVAWKFTBUTBRVLVOBRCEUSWLVAVBWK
      WJTQUTVLWGUOVNWHVPWIFVLWGVMVCVLWGVOVCVFVDABCWJVSAWGCTZUNZWHDWIEFWNWMDJTWH
      DUOAWMVEZMBCDJVMWBVGVHWNWMEKTWIEUOWONBCEKVOWDVGVHVFVIVJVK $.
      $( [20-Jul-2014] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x G $.  $d x H $.  $d x ph $.  $d x R $.
    offveq.1 $e |- ( ph -> A e. V ) $.
    offveq.2 $e |- ( ph -> F Fn A ) $.
    offveq.3 $e |- ( ph -> G Fn A ) $.
    offveq.4 $e |- ( ph -> H Fn A ) $.
    offveq.5 $e |- ( ( ph /\ x e. A ) -> ( F ` x ) = B ) $.
    offveq.6 $e |- ( ( ph /\ x e. A ) -> ( G ` x ) = C ) $.
    offveq.7 $e |- ( ( ph /\ x e. A ) -> ( B R C ) = ( H ` x ) ) $.
    $( Convert an identity of the operation to the analagous identity on the
       function operation.  (Contributed by Mario Carneiro, 24-Jul-2014.) $)
    offveq $p |- ( ph -> ( F oF R G ) = H ) $=
      ( cof co cfv inidm offn cv wcel wa ofval eqtrd eqfnfvd ) ABCGHFRSZIACCFCG
      HJJLMKKCUAZUBNABUCZCUDUEUKUITDEFSUKITACCDEFCGHJJUKLMKKUJOPUFQUGUH $.
      $( [24-Jul-2014] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d y z C $.  $d y F $.  $d y G $.  $d x y ph $.
    $d x y z R $.  $d x W $.  $d x X $.
    ofc1.1 $e |- ( ph -> A e. V ) $.
    ofc1.2 $e |- ( ph -> B e. W ) $.
    ofc1.3 $e |- ( ph -> F Fn A ) $.
    ofc1.4 $e |- ( ( ph /\ X e. A ) -> ( F ` X ) = C ) $.
    $( Left operation by a constant.  (Contributed by Mario Carneiro,
       24-Jul-2014.) $)
    ofc1 $p |- ( ( ph /\ X e. A ) ->
      ( ( ( A X. { B } ) oF R F ) ` X ) = ( B R C ) ) $=
      ( csn cxp wcel wf wfn fconstg ffn 3syl inidm wceq fvconst2g sylan ofval
      cfv ) ABBCDEBBCNZOZFGGIACHPZBUHUIQUIBRKBCHSBUHUITUALJJBUBAUJIBPIUIUGCUCKB
      CIHUDUEMUF $.
      $( [24-Jul-2014] $)
  $}

  ${
    $d w x y z F $.  $d w x y z G $.  $d w x y z H $.  $d w x y z ph $.
    $d w A $.  $d w x y z R $.  $d w x y z S $.  $d w x y z T $.
    caofcom.1 $e |- ( ph -> A e. V ) $.
    caofcom.2 $e |- ( ph -> F : A --> S ) $.
    caofcom.3 $e |- ( ph -> G : A --> S ) $.
    ${
      caofcom.4 $e |- ( ( ph /\ ( x e. S /\ y e. S ) ) ->
                        ( x R y ) = ( y R x ) ) $.
      $( Transfer a commutative law to the function operation.  (Contributed by
         Mario Carneiro, 26-Jul-2014.) $)
      caofcom $p |- ( ph -> ( F oF R G ) = ( G oF R F ) ) $=
        ( vw cfv co cmpt wcel wa wceq cv cof ffvelrn sylan jca syldan mpteq2dva
        wf caovcomg wfn ffn syl dffn5v sylib offval2 3eqtr4d ) ANDNUAZGOZUQHOZE
        PZQNDUSUREPZQGHEUBZPHGVBPANDUTVAAUQDRZURFRZUSFRZSUTVATAVCSVDVEADFGUHZVC
        VDKDFUQGUCUDZADFHUHZVCVELDFUQHUCUDZUEABCURUSFEMUIUFUGANDURUSEGHIFFJVGVI
        AGDUJZGNDURQTAVFVJKDFGUKULNDGUMUNZAHDUJZHNDUSQTAVHVLLDFHUKULNDHUMUNZUOA
        NDUSUREHGIFFJVIVGVMVKUOUP $.
        $( [26-Jul-2014] $)
    $}

    caofass.4 $e |- ( ph -> H : A --> S ) $.
    ${
      caofass.5 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ z e. S ) ) ->
                        ( ( x R y ) R z ) = ( x R ( y R z ) ) ) $.
      $( Transfer an associative law to the function operation.  (Contributed
         by Mario Carneiro, 26-Jul-2014.) $)
      caofass $p |- ( ph ->
        ( ( F oF R G ) oF R H ) = ( F oF R ( G oF R H ) ) ) $=
        ( vw co cmpt wcel cv cfv cof w3a wceq wa wf ffvelrn sylan 3jca caovassg
        syldan mpteq2dva cvv ovex a1i wfn ffn syl dffn5v sylib offval2 3eqtr4d
        ) AQEQUAZHUBZVDIUBZFRZVDJUBZFRZSQEVEVFVHFRZFRZSHIFUCZRZJVLRHIJVLRZVLRAQ
        EVIVKAVDETZVEGTZVFGTZVHGTZUDVIVKUEAVOUFZVPVQVRAEGHUGZVOVPMEGVDHUHUIZAEG
        IUGZVOVQNEGVDIUHUIZAEGJUGZVOVROEGVDJUHUIZUJABCDVEVFVHGFPUKULUMAQEVGVHFV
        MJKUNGLVGUNTVSVEVFFUOUPWEAQEVEVFFHIKGGLWAWCAHEUQZHQEVESUEAVTWFMEGHURUSQ
        EHUTVAZAIEUQZIQEVFSUEAWBWHNEGIURUSQEIUTVAZVBAJEUQZJQEVHSUEAWDWJOEGJURUS
        QEJUTVAZVBAQEVEVJFHVNKGUNLWAVJUNTVSVFVHFUOUPWGAQEVFVHFIJKGGLWCWEWIWKVBV
        BVC $.
        $( [26-Jul-2014] $)
    $}

    caofdi.5 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ z e. S ) ) ->
                        ( x T ( y R z ) ) = ( ( x T y ) R ( x T z ) ) ) $.
    $( Transfer a distributive law to the function operation.  (Contributed by
       Mario Carneiro, 26-Jul-2014.) $)
    caofdi $p |- ( ph ->
      ( F oF T ( G oF R H ) ) = ( ( F oF T G ) oF R ( F oF T H ) ) ) $=
      ( vw co wcel cv cfv cmpt cof wceq wa wf ffvelrn sylan 3jca caovdig syldan
      w3a mpteq2dva cvv ovex a1i wfn ffn syl dffn5v sylib offval2 3eqtr4d ) ARE
      RUAZIUBZVEJUBZVEKUBZFSZHSZUCREVFVGHSZVFVHHSZFSZUCIJKFUDZSZHUDZSIJVPSZIKVP
      SZVNSAREVJVMAVEETZVFGTZVGGTZVHGTZUMVJVMUEAVSUFZVTWAWBAEGIUGZVSVTNEGVEIUHU
      IZAEGJUGZVSWAOEGVEJUHUIZAEGKUGZVSWBPEGVEKUHUIZUJABCDVFVGVHGFHQUKULUNAREVF
      VIHIVOLGUOMWEVIUOTWCVGVHFUPUQAIEURZIREVFUCUEAWDWJNEGIUSUTREIVAVBZAREVGVHF
      JKLGGMWGWIAJEURZJREVGUCUEAWFWLOEGJUSUTREJVAVBZAKEURZKREVHUCUEAWHWNPEGKUSU
      TREKVAVBZVCVCAREVKVLFVQVRLUOUOMVKUOTWCVFVGHUPUQVLUOTWCVFVHHUPUQAREVFVGHIJ
      LGGMWEWGWKWMVCAREVFVHHIKLGGMWEWIWKWOVCVCVD $.
      $( [26-Jul-2014] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x G $.  $d x V $.
    $( Function analog of ~ subeq0 .  (Contributed by Mario Carneiro,
       24-Jul-2014.) $)
    ofsubeq0 $p |- ( ( A e. V /\ F : A --> CC /\ G : A --> CC ) ->
                     ( ( F oF - G ) = ( A X. { 0 } ) <-> F = G ) ) $=
      ( vx wcel cc wf cmin co cfv cc0 wceq wral wfn ffn syl eqidd wb syl2anc cv
      w3a cof csn cxp wa simp2 simp3 simp1 inidm ofval 0cn elexi adantl eqeq12d
      fvconst2 ffvelrn sylan subeq0 bitrd ralbidva fconst ax-mp eqfnfv2 3bitr4d
      offn a1i ) ADFZAGBHZAGCHZUBZEUAZBCIUCJZKZVLALUDZUEZKZMZEANZVLBKZVLCKZMZEA
      NZVMVPMZBCMZVKVRWBEAVKVLAFZUFZVRVTWAIJZLMZWBWGVNWHVQLVKAAVTWAIABCDDVLVKVI
      BAOZVHVIVJUGZAGBPQZVKVJCAOZVHVIVJUHZAGCPQZVHVIVJUIZWPAUJZWGVTRWGWARUKWFVQ
      LMVKALVLLGULUMZUPUNUOWGVTGFZWAGFZWIWBSVKVIWFWSWKAGVLBUQURVKVJWFWTWNAGVLCU
      QURVTWAUSTUTVAVKVMAOVPAOZWDVSSVKAAIABCDDWLWOWPWPWQVFXAVKAVOVPHXAALWRVBAVO
      VPPVCVGEAVMVPVDTVKWJWMWEWCSWLWOEABCVDTVE $.
      $( [24-Jul-2014] $)

    $( Function analog of ~ negsub .  (Contributed by Mario Carneiro,
       24-Jul-2014.) $)
    ofnegsub $p |- ( ( A e. V /\ F : A --> CC /\ G : A --> CC ) ->
      ( F oF + ( ( A X. { -u 1 } ) oF x. G ) ) = ( F oF - G ) ) $=
      ( vx wcel cc wf cfv cneg caddc c1 cmul cof co cmin wfn ffn syl a1i w3a cv
      csn cxp simp1 simp2 ax-1cn negcli fnconstg ax-mp simp3 inidm offn wa ofc1
      eqidd wceq ffvelrn sylan mulm1 eqtrd negsub syl2anc ofval eqtr4d offveq )
      ADFZAGBHZAGCHZUAZEAEUBZBIZVKCIZJZKBALJZUCUDZCMNOZBCPNOZDVGVHVIUEZVJVHBAQV
      GVHVIUFZAGBRSZVJAAMAVPCDDVPAQZVJVOGFZWBLUGUHZAVOGUIUJTVJVICAQVGVHVIUKZAGC
      RSZVSVSAULZUMVJAAPABCDDWAWFVSVSWGUMVJVKAFZUNZVLUPZWIVKVQIVOVMMOZVNVJAVOVM
      MCDGVKVSWCVJWDTWFWIVMUPZUOWIVMGFZWKVNUQVJVIWHWMWEAGVKCURUSZVMUTSVAWIVLVNK
      OZVLVMPOZVKVRIWIVLGFZWMWOWPUQVJVHWHWQVTAGVKBURUSWNVLVMVBVCVJAAVLVMPABCDDV
      KWAWFVSVSWGWJWLVDVEVF $.
      $( [24-Jul-2014] $)
  $}

