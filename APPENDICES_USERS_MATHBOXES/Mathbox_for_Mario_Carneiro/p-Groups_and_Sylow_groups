$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              p-Groups and Sylow groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Od $.
  $c pGrp $.
  $c pSyl $.

  $( Extend class notation to include the order function on the elements of a
     group. $)
  cgod $a class Od $.

  $( Extend class notation to include the class of all p-groups. $)
  cpgp $a class pGrp $.

  $( Extend class notation to include the class of all Sylow p-subgroups of a
     group. $)
  cslw $a class pSyl $.

  ${
    $d g h k n p s x $.
    $( Define the order of an element in a group. $)
    df-god $a |- Od = ( g e. GrpOp |-> ( x e. ran g |->
      [_ { n e. NN | ( x ( ^g ` g ) n ) = ( Id ` g ) } / s ]_
      if ( s = (/) , +oo , sup ( s , RR , `' < ) ) ) ) $.

    $( Define the set of p-groups, which are groups such that every element has
       a power of ` p ` as its order. $)
    df-pgp $a |- pGrp = { <. p , g >. | ( ( p e. Prime /\ g e. GrpOp ) /\
      A. x e. ran g E. n e. NN0 ( x ( ^g ` g ) ( p ^ n ) ) = ( Id ` g ) ) } $.

    $( Define the set of Sylow p-subgroups of a group ` g ` .  A Sylow
       p-subgroup is a p-group that is not a subgroup of any other p-groups in
       ` g ` . $)
    df-slw $a |- pSyl = ( p e. Prime , g e. GrpOp |-> { h |
      A. k ( ( h C_ k /\ k C_ g /\ p pGrp k ) <-> h = k ) } ) $.
  $}

  ${
    $d g n s x y $.  $d n x A $.  $d g E $.  $d g O $.  $d x S $.  $d g x X $.
    $d g n x G $.
    godval.1 $e |- X = ran G $.
    godval.2 $e |- E = ( ^g ` G ) $.
    godval.3 $e |- O = ( Id ` G ) $.
    $( Value of the order function. $)
    godfval $p |- ( G e. GrpOp -> ( Od ` G ) = ( x e. X |->
      if ( { n e. NN | ( x E n ) = O } = (/) , +oo ,
        sup ( { n e. NN | ( x E n ) = O } , RR , `' < ) ) ) ) $=
      ( vg vs wcel cv wceq cn c0 cpnf cr cvv cfv vy cgr crab clt ccnv csup cmpt
      co cif cgod crn rnexg syl5eqel mptexg syl cgx cgi rneq syl6eqr nnex rabex
      csb ax-17 eqeq1 supeq1 ifbieq2d csbief fveq2 oveqd eqeq12d rabbidv eqeq1d
      supeq1d syl5eq mpteq12dv df-god fvmptg mpdan ) DUBLZAFAMZBMZCUHZENZBOUCZP
      NZQWDRUDUEZUFZUIZUGZSLZDUJTWINVSFSLWJVSFDUKZSGDUBULUMAFWHSUNUOJDAJMZUKZKV
      TWAWLUPTZUHZWLUQTZNZBOUCZKMZPNZQWSRWFUFZUIZVBZUGWIUBSUJWLDNZAWMXCFWHXDWMW
      KFWLDURGUSXDXCWRPNZQWRRWFUFZUIZWHKUAWRXBXGWQBOUTVAUAMXGLKVCWSWRNWTXEXAXFQ
      WSWRPVDRWSWRWFVEVFVGXDXEWEXFWGQXDWRWDPXDWQWCBOXDWOWBWPEXDWNCVTWAXDWNDUPTC
      WLDUPVHHUSVIXDWPDUQTEWLDUQVHIUSVJVKZVLXDRWRWDWFXHVMVFVNVOAJBKVPVQVR $.
      $( [13-Jul-2014] $)

    godval.4 $e |- S = { n e. NN | ( A E n ) = O } $.
    $( Value of the order function. $)
    godval $p |- ( ( G e. GrpOp /\ A e. X ) -> ( ( Od ` G ) ` A ) =
      if ( S = (/) , +oo , sup ( S , RR , `' < ) ) ) $=
      ( vx wcel cfv wceq cn c0 cpnf cr clt cgr cgod cv co crab ccnv cif godfval
      csup cmpt fveq1d oveq1 eqeq1d rabbidv syl6eqr supeq1d ifbieq2d eqid pnfxr
      cxr elexi wor ltso cnvso mpbi supex ifex fvmpt sylan9eq ) EUAMZAGMAEUBNZN
      ALGLUCZCUCZDUDZFOZCPUEZQOZRVPSTUFZUIZUGZUJZNBQOZRBSVRUIZUGZVJAVKWALCDEFGH
      IJUHUKLAVTWDGWAVLAOZVQWBVSWCRWEVPBQWEVPAVMDUDZFOZCPUEBWEVOWGCPWEVNWFFVLAV
      MDULUMUNKUOZUMWESVPBVRWHUPUQWAURWBRWCRUTUSVASBVRSTVBSVRVBVCSTVDVEVFVGVHVI
      $.
      $( [13-Jul-2014] $)
  $}

  $(
  @{
    sylow1.1 @e |- X = ran G @.
    sylow1.2 @e |- ( ph -> G e. GrpOp ) @.
    sylow1.3 @e |- ( ph -> X e. Fin ) @.
    sylow1.4 @e |- ( ph -> P e. Prime ) @.
    @{
      sylow1.5 @e |- N = ( P pCnt ( # ` X ) ) @.
      sylow1.6 @e |- ( ph -> N e. NN ) @.
      @( Sylow's first theorem. @)
      sylow1 @p |- ( ph -> E. g e. ( P pSyl G ) ( # ` g ) = ( P ^ N ) ) @=
        ? @.

      sylow3.7 @e |- M = ( # ` ( P pSyl G ) ) @.
      @( Sylow's third theorem. @)
      sylow3a @p |- ( ph -> M || ( ( # ` X ) / ( P ^ N ) ) ) @=
        ? @.

      @( Sylow's third theorem. @)
      sylow3b @p |- ( ph -> ( M mod P ) = 1 ) @=
        ? @.
    @}

    sylow2.5 @e |- ( ph -> H e. ( P pSyl G ) ) @.
    sylow2.6 @e |- ( ph -> K e. ( P pSyl G ) ) @.
    sylow2.7 @e |- N = ( inv ` G ) @.
    sylow2.8 @e |- T = { x e. G | ( ( g G x ) D g ) e. H } @.
    @( Sylow's second theorem. @)
    sylow2 @p |- ( ph -> E. g e. G T = K ) @=
      ? @.
  @}
  $)

