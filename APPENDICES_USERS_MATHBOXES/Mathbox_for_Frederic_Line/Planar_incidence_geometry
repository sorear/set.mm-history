$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Planar incidence geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d L a b l x $.  $d P a b x $.
    efp.1 $e |- P = U. L $.
    $( Euclid's first postulate.  There is a unique line passing through two
       distinct points.  (Contributed by FL, 2-Aug-2009.) $)
    efp $p |- ( L e. Plig -> A. a e. P A. b e. P ( a =/= b
        -> E! l e. L ( a e. l /\ b e. l ) ) ) $=
      ( vx cplig wcel cv wne wa wreu wi wral w3a wrex wn isplig ibi simp1d ) BH
      IZCJZDJZKZUCEJZIZUDUFIZLEBMNDAOCAOZUEUGUHPDAQCAQEBOZUGUHGJUFIPREBOGAQDAQC
      AQZUBUIUJUKPHABCDGEFSTUA $.
      $( [2-Aug-2009] $)

  $}

  ${
    $d A a b l $.  $d B b l $.  $d L a b l $.  $d P a b $.
    efp2.1 $e |- P = U. L $.
    $( Euclid's first postulate.  There is a unique line passing through two
       distinct points.  (Contributed by FL, 2-Aug-2009.) $)
    efp2 $p |- ( ( L e. Plig /\ A e. P /\ B e. P ) -> ( A =/= B
        -> E! l e. L ( A e. l /\ B e. l ) ) ) $=
      ( va vb cplig wcel wne cv wa wreu wi wral wceq eleq1 reubidv imbi12d 3imp
      efp neeq1 anbi1d neeq2 anbi2d rcla42v ex com3r syl ) DIJZACJZBCJZABKZAELZ
      JZBUOJZMZEDNZOZUKGLZHLZKZVAUOJZVBUOJZMZEDNZOZHCPGCPZULUMUTOOCDGHEFUBULUMV
      IUTULUMVIUTOVHUTAVBKZUPVEMZEDNZOGHABCCVAAQZVCVJVGVLVAAVBUCVMVFVKEDVMVDUPV
      EVAAUORUDSTVBBQZVJUNVLUSVBBAUEVNVKUREDVNVEUQUPVBBUORUFSTUGUHUIUJUA $.
      $( [2-Aug-2009] $)
  $}

  ${
    $d L a b l x $.  $d P a b x $.
    l2p.1 $e |- P = U. L $.
    $( Any line has at least two distinct points.  (Contributed by FL,
       2-Aug-2009.) $)
    l2p $p |- ( L e. Plig -> A. l e. L E. a e. P E. b e. P
       ( a =/= b /\ a e. l /\ b e. l ) ) $=
      ( vx cplig wcel cv wne wa wreu wi wral w3a wrex wn isplig ibi simp2d ) BH
      IZCJZDJZKZUCEJZIZUDUFIZLEBMNDAOCAOZUEUGUHPDAQCAQEBOZUGUHGJUFIPREBOGAQDAQC
      AQZUBUIUJUKPHABCDGEFSTUA $.
      $( [2-Aug-2009] $)
  $}

  $c line $.

  $( Extend class notation to include a function whose value is the line. $)
  cline $a class line $.

  ${
    $d x y p q l $.
    $( If ` x ` is a planar incidence geometry, and ` p ` and ` q ` are two
       points of the plane, ` ( ( line `` x ) `` <. p , q >. ) ` is the line
       passing through ` p ` and ` q ` . $)
    df-line $a |- line = { <. x , y >. | ( x e. Plig /\ y = { <. <. p ,
       q >. , l >. | ( ( p e. U. x /\ q e. U. x ) /\ ( p =/= q /\ ( l
       e. x /\ p e. l /\ q e. l ) ) ) } ) } $.
  $}

  ${
    $d L l p q x y $.  $d P l p q x y $.
    isline1.2 $e |- P = U. L $.
    $( The value of the function ` ( line `` L ) ` is the line passing through
       the points ` p ` and ` q ` .  (Contributed by FL, 24-Apr-2011.) $)
    isline1 $p |- ( L e. Plig -> ( line ` L ) = { <. <. p , q >. , l
       >. | ( ( p e. P /\ q e. P ) /\ ( l e. L /\ ( p =/= q /\ p e. l
       /\ q e. l ) ) ) } ) $=
      ( vx cplig wcel cv wa w3a cvv wmo wi ex simpl 3jca jca anim12d wne copab2
      cline cfv wceq wfun cdm wal weu wreu efp2 imp 3anass df-reu bitr4i sylibr
      vy eubii eumo syl 3expib simpr1 simpr2 simpr3 mobii moanimv bitri syl6ibr
      impbii alrimivv funoprabg cxp wss dmoprabss uniexg syl5eqel xpexg syl2anc
      cuni ssexg sylancr funex unieq syl6eqr eleq2d biimpd eleq2 biimpcd adantl
      3ad2ant1 impcom biimprd simprr1 biimprcd adantr simprr2 simprr3 oprabbidv
      impbid df-line fvopab4g mpdan ) BHIZDJZAIZCJZAIZKZEJZBIZXDXFUAZXDXIIZXFXI
      IZLZKZKZDCEUBZMIZBUCUDXQUEXCXQUFZXQUGZMIZXRXCXPENZCUHDUHXSXCYBDCXCXHXOENZ
      OYBXCXHXKXJXLXMLZENZOZYCXCXEXGYFXCXEXGLZXKYEYGXKKZYDEUIZYEYHXLXMKZEBUJZYI
      YGXKYKXDXFABEFUKULYIXJYJKZEUIYKYDYLEXJXLXMUMURYJEBUNUOUPYDEUSUTPVAYCXKYDK
      ZENYFXOYMEXOYMXOXKYDXJXKXLXMVBXOXJXLXMXJXNQXJXKXLXMVCXJXKXLXMVDRSYMXJXNXK
      XJXLXMVBYMXKXLXMXKYDQXKXJXLXMVCXKXJXLXMVDRSVIVEXKYDEVFVGVHXHXOEVFUPVJXPDC
      EVKUTXCXTAAVLZVMYNMIZYAXODCEAAVNXCAMIZYPYOXCABVSZMFBHVOVPZYRAAMMVQVRXTYNM
      VTWAMXQWBVRGUQBXDGJZVSZIZXFYTIZKZXKXIYSIZXLXMLZKZKZDCEUBXQHMUCYSBUEZUUGXP
      DCEUUHUUGXPUUHUUCXHUUFXOUUHUUAXEUUBXGUUHUUAXEUUHYTAXDUUHYTYQAYSBWCFWDZWEZ
      WFUUHUUBXGUUHYTAXFUUIWEZWFTUUHUUFXOUUHUUFKXJXNUUFUUHXJUUEUUHXJOZXKUUDXLUU
      LXMUUHUUDXJYSBXIWGZWHWJWIWKUUFXNUUHUUFXKXLXMXKUUEQXKUUDXLXMVCXKUUDXLXMVDR
      WISPTUUHXHUUCXOUUFUUHXEUUAXGUUBUUHUUAXEUUJWLUUHUUBXGUUKWLTUUHXOUUFUUHXOKZ
      XKUUEXKXLXMXJUUHWMUUNUUDXLXMXOUUHUUDXJUUHUUDOXNUUHUUDXJUUMWNWOWKXKXLXMXJU
      UHWPXKXLXMXJUUHWQRSPTWSWRGUQCDEWTXAXB $.
      $( [24-Apr-2011] $)
  $}

