$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              General Set Theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c x u v $.  $d a b x C $.  $d a b c x B $.  $d a b x u v A $.
    $d R a b u v x $.  $d C a b c x $.
    uninqs.1 $e |- Er R $.
    $( Class union distributes over the intersection of two subclasses of a
       quotient space.  Compare ~ uniin .  (Contributed by FL, 25-May-2007.) $)
    uninqs $p |- ( ( B C_ ( A /. R ) /\ C C_ ( A /. R ) ) ->
      U. ( B i^i C ) = ( U. B i^i U. C ) ) $=
      ( vc vb va vu vv wss wa wceq cv wcel wi sylbi ex com23 imp vx cqs cin wex
      cuni eluni wal dfss2 pm3.35 cec wrex vex eleq2 biimpa c0 wne inelcm df-ne
      elqs wn wo erdisj pm5.61 eqeq12 biimprcd adantr mpan syl an4s exp32 com24
      syl2an pm2.43i rexlimivw com3l com13 imp31 eleq1 elequ2 biimprd wb eleq2i
      elin incom syl6bb equcoms biimpd anim12d a4imev expcom sylbir syl6bi syl9
      com3r pm2.43a com14 com4t com12 exlimiv syl7bi syl5bi imp3a 3imtr4g ssrdv
      a4s uniin jctil eqss sylibr ) BADUBZKZCXJKZLZBCUCZUEZBUEZCUEZUCZKZXRXOKZL
      XOXRMXMXTXSXMUAXRXOXMUANZXPOZYAXQOZLYAFNZOZYDXNOZLZFUDZYAXROYAXOOXMYBYCYH
      YBYAGNZOZYIBOZLZGUDZXMYCYHPGYABUFYCYAHNZOZYNCOZLZHUDZXMYMYHHYACUFYMXMYRYH
      PZYLXMYSPGYRXMYLYHYQXMYLYHPZPHXMYQYTXKXLYQYTPZXKYKYIXJOZPZGUGXLUUAPZGBXJU
      HUUCUUDGXLUUCUUAXLYPYNXJOZPZHUGUUCUUAPZHCXJUHUUFUUGHYQYLUUFUUCYHYOYPYLUUF
      UUCYHPZPZPYLYPYOUUIYJYKYPYOUUIPZPZYJYKUUKPYKYJYJUUKYJYKYJUUKPZYKYJYKUULPY
      PYKYJYKYJLZUUJYKYPYJUUMUUJPZPZYPYKYPUUOPYPYKLZYJYPUUNUUPYJYPUUNPYPUUMUUPY
      JLZUUJYPUUMUUQUUJPYPUUMLZYOUUQUUIUURYOUUQUUIPUURYOLZUUFUUQUUHUUSUUFUUQUUH
      PUUSUUFLUUCYNYIMZUUQYHUURYOUUFUUCUUTPUURUUCUUFYOUUTYPUUMUUCUUFYOUUTPZPPYP
      UUFUUCUUMUVAYPUUFUUCUUMUVAPPZYPUUFLUUEUVBYPUUEUIUUMUUCUUEUVAYKYJUUCUUEUVA
      PZPYKUUCYJUVCYKUUCYJUVCPZYKUUCLUUBUVDYKUUBUIUUBYIINZDUJZMZIAUKUVDIAYIDGUL
      USUVGUVDIAUUEUVGYJUVAUUEYNJNZDUJZMZJAUKUVGYJUVAPZPZJAYNDHULUSUVJUVLJAUVJU
      VGUVKUVJUVGLZUVKUVMYOYJUVMUUTUVMYOYJUVMUUTPZUVJYOUVGYJUVNUVJYOLYAUVIOZYAU
      VFOZUVNUVGYJLUVJYOUVOYNUVIYAUMUNUVGYJUVPYIUVFYAUMUNUVOUVPLUVIUVFUCZUOUPZU
      VNYAUVIUVFUQUVRUVQUOMZUTZUVNUVQUOURUVIUVFMZUVSVAZUVTUVNUVHUVEDJULIULEVBUW
      BUVTLUWAUVTLUVNUWAUVSVCUWAUVNUVTUVMUUTUWAYNUVIYIUVFVDVEVFQVGQVHVLVIVJVKVM
      RVNQVOVNQVHRSTVPVHRVKTVKVQUUPYJUUTYHPUUPUUTYJYHYPYKUUTYJYHPZPUUTYPYKUWCUU
      TYPYICOZYKUWCPYNYICVRUWDYKUWCUWDYKLYICBUCZOZUWCYICBWCYJUWFYHYJUWFLYGFGYDY
      IMZYJYEUWFYFUWGYEYJFGUAVSVTUWGUWFYFUWFYFWAGFYIYDMUWFYDUWEOYFYIYDUWEVRUWEX
      NYDCBWDWBWEWFWGWHWIWJWKRWLVOTSTWMRSRSRWNRSRWOWPRWOVPVMTVPTWQXEQWRXEQTWRWS
      VPWSWRWTXAXBYAXPXQWCFYAXNUFXCXDBCXFXGXOXRXHXI $.
      $( [4-Jun-2007] $) $( [25-May-2007] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Inference from membership to difference.  (Contributed by FL,
       6-Jun-2007.)  (The proof was shortened by Andrew Salmon, 9-Jul-2011.) $)
    difeqri2 $p |- ( A. x ( ( x e. A /\ -. x e. B ) <-> x e. C )
      -> ( A \ B ) = C ) $=
      ( cv wcel wn wa wb wal cdif wceq eldif bibi1i albii dfcleq biimpri sylbir
      ) AEZBFSCFGHZSDFZIZAJSBCKZFZUAIZAJZUCDLZUEUBAUDTUASBCMNOUGUFAUCDPQR $.
      $( [9-Jul-2011] $) $( [6-Jun-2007] $)

  $}

  ${
    $d A u v w y z $.  $d B u v w y z $.  $d C u v w y z $.  $d D u v w y z $.
    $d ph u x $.  $d ta u v w y z $.  $d v w x y z $.
    elo.1 $e |- ( y = A -> ( ph <-> ps ) ) $.
    elo.2 $e |- ( z = B -> ( ps <-> ch ) ) $.
    elo.3 $e |- ( v = C -> ( ch <-> th ) ) $.
    elo.4 $e |- ( w = D -> ( th <-> ta ) ) $.
    $( The law of concretion for operation class abstraction.  Compare with
       ~ eloprabg .  This version is to be used with categories.  (Contributed
       by FL, 14-Jul-2007.) $)
    elo $p |- ( ( ( A e. Q /\ B e. R /\ C e. S ) /\ D e. T ) ->
      ( <. <. A , B >. , <. C , D >. >. e. { x | E. y E. z E. v E. w
      ( x = <. <. y , z >. , <. v , w >. >. /\ ph ) } <-> ta ) ) $=
      ( vu wcel w3a cvv cop cv wceq wa wex cab wb elex 3anim123i wi eqeq1 eqcom
      syl6bb cxp simpl2 opelxpi 3ad2antl3 simpr vex otthg 3adant3 opthg 3anbi3d
      and4as 3ad2ant3 bitrd syl3anc sylan9bbr syl3an9b sylan9bb pm5.32i 4exbidv
      opex anbi1d eleq1 syl5bbr adantl elisset anim12i eeeeanv sylibr biantrurd
      elab 19.41vvvv syl6rbbr adantr 3bitr3d expcom vtocle syl2an ) KOUDZLPUDZM
      QUDZUEKUFUDZLUFUDZMUFUDZUEZNUFUDZKLUGZMNUGZUGZFUHZGUHZHUHZUGJUHZIUHZUGZUG
      ZUIZAUJZIUKJUKHUKGUKZFULZUDZEUMZNRUDWQWTWRXAWSXBKOUNLPUNMQUNUONRUNXCXDUJZ
      XTUPUCXGXEXFVSYAUCUHZXGUIZXTYAYCUJZYBXNUIZAUJZIUKJUKHUKGUKZXIKUIZXJLUIZXK
      MUIZUEZXLNUIZUJZEUJZIUKJUKHUKGUKZXSEYDYFYNGHJIYDYFYMAUJYNYDYEYMAYCYEXNXGU
      IZYAYMYCYEXGXNUIYPYBXGXNUQXGXNURUSYAXAXFUFUFUTZUDZXDYPYMUMWTXAXBXDVAXBWTX
      DYRXAMNUFUFVBVCXCXDVDXAYRXDUEYPYHYIXMXFUIZUEZYMXAYRYPYTUMXDXIXJKLXMXFUFYQ
      GVEHVEXKXLVSVFVGXDXAYTYMUMYRXDYTYHYIYJYLUJZUEYMXDYSUUAYHYIXKXLMNUFJVEIVEV
      HVIYHYIYJYLVJUSVKVLVMVNVTYMAEYKADYLEYHABYICYJDSTUAVOUBVPVQUSVRYCYGXSUMYAY
      GYBXRUDYCXSXQYGFYBUCVEXHYBUIZXPYFGHJIUUBXOYEAXHYBXNUQVTVRWIYBXGXRWAWBWCYA
      YOEUMYCYAEYMIUKJUKHUKGUKZEUJYOYAUUCEYAYHGUKZYIHUKZYJJUKZUEZYLIUKZUJUUCXCU
      UGXDUUHWTUUDXAUUEXBUUFGKUFWDHLUFWDJMUFWDUOINUFWDWEYHYIYJYLHJIGWFWGWHYMEHJ
      IGWJWKWLWMWNWOWP $.
      $( [14-Jul-2007] $)
  $}


  $( An intersection with a member of a powerset belongs to this powerset.
     (Contributed by FL, 25-Sep-2007.) $)
  inpws1 $p |- ( A e. ~P C -> ( A i^i B ) e. ~P C ) $=
    ( cin cvv wcel cpw inex1g wss elpwi ssinss1 syl elpwg syl5ibr mpcom ) ABDZE
    FZACGZFZPRFZABRHSTQPCIZSACIUAACJABCKLPCEMNO $.
    $( [25-Sep-2007] $)

  $( An intersection with a member of a powerset belongs to this powerset.
     (Contributed by FL, 26-Oct-2007.) $)
  inpws2 $p |- ( B e. ~P C -> ( A i^i B ) e. ~P C ) $=
    ( cpw wcel cin incom inpws1 syl5eqel ) BCDZEABFBAFJABGBACHI $.
    $( [26-Oct-2007] $)

  ${
    $d v x $.  $d w x $.  $d x y $.  $d x z $.
    $( Structure of the class abstraction used by ` Alg ` , ` Cat ` and
       ` Ded ` .  (Contributed by FL, 26-Oct-2007.) $)
    stcat $p |- { x | E. y E. z E. v E. w ( x = <. <. y , z >. , <. v , w >. >.
       /\ ph ) } C_ ( ( _V X. _V ) X. ( _V X. _V ) ) $=
      ( cv cop wceq wa wex cvv cxp wcel opex opelxp vex mpbir2an eleq1 exlimivv
      mpbiri adantr abssi ) BGZCGZDGZHZFGZEGZHZHZIZAJZEKFKZDKCKBLLMZUOMZUNUDUPN
      ZCDUMUQFEULUQAULUQUKUPNZURUGUONZUJUONZUGUJUOUOUHUIOPUSUELNUFLNUEUFLLDQZPC
      QVARUTUHLNUILNUHUILLEQZPFQVBRRUDUKUPSUAUBTTUC $.
      $( [26-Oct-2007] $)
  $}

  $( A theorem of the ~ 1st2nd family.  (Contributed by FL, 26-Oct-2007.) $)
  11st22nd $p |- ( ( ( Rel B /\ Rel dom B /\ Rel ran B ) /\ A e. B ) ->
     A = <. <. ( 1st ` ( 1st ` A ) ) , ( 2nd ` ( 1st ` A ) ) >. ,
            <. ( 1st ` ( 2nd ` A ) ) , ( 2nd ` ( 2nd ` A ) ) >. >. ) $=
    ( wrel cdm crn w3a wcel wa c1st cfv c2nd cop wceq 1st2nd 3ad2antl1 wi exp32
    sylan2 a1i 3imp1 1stdm com13 2ndrn com3r opeq12d eqtrd ) BCZBDZCZBEZCZFABGZ
    HZAAIJZAKJZLZUNIJUNKJLZUOIJUOKJLZLUGUIULAUPMUKABNOUMUNUQUOURUGUIUKULUNUQMZU
    KUIUGULUSPZUIUGUTPPUKUIUGULUSUGULHZUIUNUHGUSABUAUNUHNRQSUBTUGUIUKULUOURMZUI
    UKUGULVBPZUKUGVCPPUIUKUGULVBVAUKUOUJGVBABUCUOUJNRQSUDTUEUF $.
    $( [26-Oct-2007] $)

  ${
    $d A x $.
    $( The union of a part of a powerset belongs to it.  (Contributed by FL,
       16-Nov-2007.) $)
    ump $p |- ( A e. V -> U. { x e. ~P A | ph } e. ~P A ) $=
      ( wcel cpw crab cuni wss ssrab2 sspwuni mpbi elpw2g mpbiri ) CDEABCFZGZHZ
      OEQCIZPOIRABOJPCKLQCDMN $.
      $( [29-Nov-2007] $) $( [16-Nov-2007] $)
  $}

  $( Moving an element ` B ` out from the intersection of a class ` A ` .
     (Contributed by FL, 29-Nov-2007.) $)
  moec $p |- ( B e. A -> |^| A = ( B i^i |^| ( A \ { B } ) ) ) $=
    ( wcel cint csn cdif cun cin difsnid eqcomd inteqd wceq a1i cvv elex c0 cif
    intun sneq id eqeq12d 0ex elimel intsn dedth syl ineq2d incom syl6eq 3eqtrd
    ) BACZADABEZFZULGZDZUMDZULDZHZBUPHZUKAUNUKUNAABIJKUOURLUKUMULRMUKURUPBHUSUK
    UQBUPUKBNCZUQBLZBAOUTVAUTBPQZEZDZVBLBPBVBLZUQVDBVBVEULVCBVBSKVETUAVBBPNUBUC
    UDUEUFUGUPBUHUIUJ $.
    $( [29-Nov-2007] $)

  ${
    $d A x y $.  $d B x y $.  $d C y $.
    $( Splitting an intersection.  (Contributed by FL, 15-Oct-2012.) $)
    splint $p |- ( B C_ A ->
    |^|_ x e. A C = ( |^|_ x e. ( A \ B ) C i^i |^|_ x e. B C ) ) $=
      ( vy wss cv ciin wcel cdif cin wb wceq wral wa cvv eliin ax-mp a1i cun
      wal undif equncom biimpi eqcoms sylbi raleqdv ralunb syl6bb bicomi bicomd
      vex anbi12d elin syl6bbr 3bitrd alrimiv dfcleq sylibr ) CBFZEGZABDHZIZVAA
      BCJZDHZACDHZKZIZLZEUAVBVGMUTVIEUTVCVADIZABNZVJAVDNZVJACNZOZVHVCVKLZUTVAPI
      ZVOEULZAVABDPQRSUTVKVJAVDCTZNVNUTVJABVRUTCVDTZBMBVRMZCBUBVTBVSBVSMVTBCVDU
      CUDUEUFUGVJAVDCUHUIUTVNVAVEIZVAVFIZOVHUTVLWAVMWBVLWALUTWAVLVPWAVLLVQAVAVD
      DPQRUJSUTWBVMWBVMLZUTVPWCVQAVACDPQRSUKUMVAVEVFUNUOUPUQEVBVGURUS $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d A x $.  $d B x $.  $d D x $.
    splintx.1 $e |- ( x = B -> C = D ) $.
    $( Moving an element out from an intersection.  (Contributed by FL,
       15-Oct-2012.) $)
    splintx $p |- ( B e. A ->
     |^|_ x e. A C = ( |^|_ x e. ( A \ { B } ) C i^i D ) ) $=
      ( wcel ciin csn cdif cin wss wceq snssi splint syl iinxsng ineq2d eqtrd )
      CBGZABDHZABCIZJDHZAUBDHZKZUCEKTUBBLUAUEMCBNABUBDOPTUDEUCACDEBFQRS $.
      $( [15-Oct-2012] $)
  $}

  ${
    $( A function's value belongs to its range.  A more general version of
       ~ fnovrn .  To be used with partial operations.  (Contributed by FL,
       10-Mar-2008.) $)
    fnovrn2 $p |- ( ( Fun F /\ <. A , B >. e. dom F ) ->
      ( A F B ) e. ran F ) $=
      ( wfun cop cdm wcel wa co cfv crn df-ov fvelrn syl5eqel ) CDABEZCFGHABCIO
      CJCKABCLOCMN $.
      $( [10-Mar-2008] $)
  $}

  $( If an intersection is not empty its operands are not empty.  (Contributed
     by FL, 27-Apr-2008.) $)
  neiopne $p |- ( ( A i^i B ) =/= (/) -> ( A =/= (/) /\ B =/= (/) ) ) $=
    ( cin c0 wceq wn wo wne wa ineq1 incom eqtr in0 syl6eq mpan2 syl ineq2 jaoi
    con3i df-ne neanior 3imtr4i ) ABCZDEZFADEZBDEZGZFUCDHADHBDHIUGUDUEUDUFUEUCD
    BCZEZUDADBJUIUHBDCZEZUDDBKUIUKIUCUJDUCUHUJLBMNOPUFUCADCDBDAQAMNRSUCDTADBDUA
    UB $.
    $( [27-Apr-2008] $)

  $( Image of a pre-image.  (Contributed by FL, 1-Jun-2008.) $)
  f2imacnv $p |- ( ( F : A -1-1-onto-> B /\ C C_ B ) ->
                 ( F " ( `' F " C ) ) = C ) $=
    ( wf1o wss ccnv cima wceq wf1 wi f1ocnv f1of1 wa imacnvcnv f1imacnv syl5eqr
    ex 3syl imp ) ABDEZCBFZDDGZCHZHZCIZUABAUCEBAUCJZUBUFKABDLBAUCMUGUBUFUGUBNUE
    UCGUDHCDUDOBACUCPQRST $.
    $( [1-Jun-2008] $)


  $( Symmetrical equality of the images and of their antecedents when the
     mapping is one to one.  (Contributed by FL, 1-Jun-2008.) $)
  oooeqim2 $p |- ( ( F : A -1-1-> B /\ X C_ A /\ Y C_ A )
     -> ( ( F " X ) = ( F " Y ) <-> X = Y ) ) $=
    ( wf1 wss w3a cima wceq ccnv wa f1imacnv ex anim12d 3impib imaeq2 eqtr syl
    wi com23 eqcoms imp syl2im impbid1 ) ABCFZDAGZEAGZHZCDIZCEIZJZDEJZUICKZUJIZ
    DJZUNUKIZEJZLZULUOUQJZUMUFUGUHUSUFUGUPUHURUFUGUPABDCMNUFUHURABECMNOPUJUKUNQ
    UPURUTUMTZURVATDUODUOJZUTURUMVBUTURUMTZVBUTLDUQJZVCDUOUQRVDURUMDUQERNSNUAUB
    UCUDDECQUE $.
    $( [1-Jun-2008] $)

  $( A theorem about things which don't exist ` _V ` and ` ( _V X. _V ) ` .
     (Contributed by FL, 22-Sep-2008.) $)
  vxveqv $p |- ( _V X. _V ) =/= _V $=
    ( vx cvv cxp wne wceq wn wpss wss dfpss3 ssv cv wcel wa wex c0 0nelxp eleq1
    0ex notbid mpbir anbi12d cla4ev mp2an nss mpbir2an pssv mpbi df-ne ) BBCZBD
    UIBEFZUIBGZUJUKUIBHBUIHFZUIBIUIJULAKZBLZUMUILZFZMZANZOBLZOUILZFZURRBBPUQUSV
    AMAORUMOEZUNUSUPVAUMOBQVBUOUTUMOUIQSUAUBUCABUIUDTUEUIUFUGUIBUHT $.
    $( [23-Sep-2008] $) $( [22-Sep-2008] $)

  $( The double union of the converse of a class ` A ` is included in the
     double union of the class.  (Contributed by FL, 31-Jul-2009.) $)
  ducidu $p |- U. U. `' A C_ U. U. A $=
    ( ccnv cuni cdm crn cun unidmrn dmrnssfld eqsstri ) ABCCADAEFACCAGAHI $.
    $( [31-Jul-2009] $)


  $( The field of a class equals the field of the its converse.  (Contributed
     by FL, 16-Apr-2012.) $)
  fldcnv $p |- ( dom A u. ran A ) = ( dom `' A u. ran `' A ) $=
    ( cdm crn cun ccnv uncom df-rn dfdm4 uneq12i eqtri ) ABZACZDLKDAEZBZMCZDKLF
    LNKOAGAHIJ $.
    $( [16-Apr-2012] $)

  ${
    $d R x $.
    $( The domain of a reflexive class equals its field.  (Contributed by FL,
       29-Dec-2011.) $)
    domfldrefc $p |- ( A. x e. ( dom R u. ran R ) x R x
      -> dom R = ( dom R u. ran R ) ) $=
      ( cv wbr cdm crn cun wral wss wa wceq wcel wi wal vex breldm ralimi sylib
      df-ral sylibr dfss2 ssun1 jctil eqss ) ACZUEBDZABEZBFZGZHZUGUIIZUIUGIZJUG
      UIKUJULUKUJUEUILUEUGLZMANZULUJUMAUIHUNUFUMAUIUEUEBAOPQUMAUISRAUIUGUATUGUH
      UBUCUGUIUDT $.
      $( [29-Dec-2011] $)

    $( The range of a reflexive class equals its field.  (Contributed by FL,
       29-Dec-2011.) $)
    ranfldrefc $p |- ( A. x e. ( dom R u. ran R ) x R x
      -> ran R = ( dom R u. ran R ) ) $=
      ( cv wbr cdm crn cun wral wss wa wceq wcel wi wal vex brelrn ralimi sylib
      df-ral sylibr dfss2 ssun2 jctil eqss ) ACZUEBDZABEZBFZGZHZUHUIIZUIUHIZJUH
      UIKUJULUKUJUEUILUEUHLZMANZULUJUMAUIHUNUFUMAUIUEUEBAOZUOPQUMAUISRAUIUHUATU
      HUGUBUCUHUIUDT $.
      $( [29-Dec-2011] $)

    $( The domain and range of a reflexive class are equal.  (Contributed by
       FL, 29-Dec-2011.) $)
    dranfldrefc $p |- ( A. x e. ( dom R u. ran R ) x R x
      -> dom R = ran R ) $=
      ( cv wbr cdm crn cun wral domfldrefc ranfldrefc eqtr4d ) ACZLBDABEZBFZGZH
      MONABIABJK $.
      $( [29-Dec-2011] $)
  $}

  ${
    $d R x $.
    $( Domain and range of a reflexive relation are equal.  (Contributed by FL,
       6-Oct-2008.) $)
    domrngref $p |- ( ( Rel R /\ A. x e. U. U. R x R x ) -> dom R = ran R ) $=
      ( wrel cv wbr cuni wral wa cdm wcel crn wal wceq df-ral cun relfld imim2i
      wb wi sylbi eqcomd eleq2d biimpd imim1d wo elun imbi1i jaob brelrn adantr
      vex breldm adantl impbid syl6 alimdv syl5bi imp dfcleq sylibr ) BCZADZVBB
      EZABFFZGZHVBBIZJZVBBKZJZRZALZVFVHMVAVEVKVEVBVDJZVCSZALVAVKVCAVDNVAVMVJAVA
      VMVBVFVHOZJZVCSZVJVAVOVLVCVAVOVLVAVNVDVBVAVDVNBPUAUBUCUDVPVGVIUEZVCSZVJVO
      VQVCVBVFVHUFUGVRVGVCSZVIVCSZHZVJVGVCVIUHWAVGVIVSVGVISVTVCVIVGVBVBBAUKZWBU
      IQUJVTVIVGSVSVCVGVIVBVBBWBULQUMUNTTUOUPUQURAVFVHUSUT $.
      $( [7-Oct-2008] $) $( [6-Oct-2008] $)

    $( The domain of a reflexive relation is equal to its field .  (Contributed
       by FL, 6-Oct-2008.) $)
    domfldref $p |- ( ( Rel R /\ A. x e. U. U. R x R x ) -> dom R = U. U. R )
        $=
      ( wrel cv wbr cuni wral cdm wceq crn cun wa relfld domrngref uneq2 eqcomd
      wi eqtr unidm syl5com syl6req ex syl anabsi5 ) BCZADZUFBEABFFZGZBHZUGIZUE
      UGUIBJZKZIZUEUHLZUJBMUNUIUKIZUMUJQABNUOULUIUIKZIZUMUJUOUPULUIUKUIOPUMUQUJ
      UMUQLUGUPUIUGULUPRUISUAUBTUCTUD $.
      $( [7-Oct-2008] $) $( [6-Oct-2008] $)
  $}

  ${
    $d A x $.  $d R x $.
    $( In a reflexive class ` R ` , an element ` A ` belongs to the field iff
       the pair ` <. A , A >. ` belongs to ` R ` .  (Contributed by FL,
       30-Dec-2011.) $)
    domintreflemb $p |- ( ( A e. B /\ A. x e. dom R x R x ) ->
      ( A e. dom R <-> A R A ) ) $=
      ( wcel cv wbr cdm wral wa wi id breq12d rcla4cva ex adantl breldmg adantr
      wceq impbid ) BCEZAFZUBDGZADHZIZJBUDEZBBDGZUEUFUGKUAUEUFUGUCUGABUDUBBSZUB
      BUBBDUHLZUIMNOPUAUGUFKUEUAUGUFBBCDQORT $.
      $( [30-Dec-2011] $)
  $}

  ${
    $d R x $.  $d S x $.
    $( The domain of the intersection of two reflexive classes is the
       intersection of their domains.  Compare with ~ dmin .  (Contributed by
       FL, 30-Dec-2011.) $)
    domintrefb $p |- ( ( A. x e. dom R x R x /\
     A. x e. dom S x S x ) ->
      dom ( R i^i S ) = ( dom R i^i dom S ) ) $=
      ( cv wbr cdm wral wa cin wss wceq inss1 sseli imim1i ralimi2 inss2 r19.26
      wcel sylbir sylibr brin vex breldm ralimi dfss3 syl2an dmin jctil eqss )
      ADZUJBEZABFZGZUJUJCEZACFZGZHZBCIZFZULUOIZJZUTUSJZHUSUTKUQVBVAUMUKAUTGZUNA
      UTGZVBUPUKUKAULUTUJUTRZUJULRUKUTULUJULUOLMNOUNUNAUOUTVEUJUORUNUTUOUJULUOP
      MNOVCVDHUKUNHZAUTGZVBUKUNAUTQVGUJUSRZAUTGVBVFVHAUTVFUJUJUREVHUJUJBCUAUJUJ
      URAUBUCSUDAUTUSUETSUFBCUGUHUSUTUIT $.
      $( [30-Dec-2011] $)
  $}

  ${
    $d A x y $.  $d R x y $.
    $( If ` R ` is a reflexive relation and ` A ` a part of its field, ` A ` is
       a part of the image of ` A ` by ` R ` .  (Contributed by FL,
       3-Jul-2009.) $)
    imgfldref2 $p |- ( ( A. x e. U. U. R x R x /\ A C_ U. U. R )
    -> A C_ ( R " A ) ) $=
      ( vy cv wbr cuni wral wss wa wrex cab cima wcel wi wal hbra1 hban syl6com
      ax-17 ssel ra4 breq1 rcla4e expcom com24 pm2.43i com3l alrimi ssab sylibr
      imp dfima2 syl6sseqr ) AEZUOCFZACGGZHZBUQIZJZBDEZUOCFZDBKZALZCBMUTUOBNZVC
      OZAPBVDIUTVFAURUSAUPAUQQUSATRURUSVFVEURUSVCVEURUSVCOOVEUSURVEVCUSVEUOUQNZ
      URVFOBUQUOUAURVGUPVFUPAUQUBVEUPVCVBUPDUOBUPDTVAUOUOCUCUDUESSUFUGUHULUIVCA
      BUJUKDACBUMUN $.
      $( [3-Jul-2009] $)
  $}


  $( ` <. A , A >. ` belongs to a restriction of the identity class iff ` A `
     belongs to the restricting class.  (Contributed by FL, 27-Oct-2008.) $)
  eltids $p |- ( A e. V -> ( A e. B <-> <. A , A >. e. ( _I |` B ) ) ) $=
    ( wcel cop cid wa cres wbr ididg df-br sylib biantrurd opelresg bitr4d ) AC
    DZABDZAAEZFDZQGRFBHDPSQPAAFISACJAAFKLMAAFBCNO $.
    $( [17-Nov-2008] $) $( [27-Oct-2008] $)

  ${
    $d A u x y $.  $d R u x y $.
    $( Two ways to state a relation is reflexive.  (Adapted from Tarski.)
       (Contributed by FL, 15-Jan-2012.) $)
    ref4w $p |- ( A. x e. A x R x
     <-> ( _I |` A ) C_ R ) $=
      ( vy vu cv wbr wral wcel wi wal cop cid wss df-ral cvv eltids ax-mp sylbi
      vex cres wb df-br imbi12i albii ralidm ralv bitri cxp pm2.27 opelresg weq
      wa ideq opeq2 eleq1d biimpcd syl6bi pm2.43i com3r sylbir imp ralrimiv a4s
      syl6 ralimi wceq eleq1 imbi12d ralxp sylibr wrel relres df-rel mpbi sseli
      ancri pm3.31 syl5 alimi syl dfss2 ssel alrimiv impbii 3bitri ) AFZWGCGZAB
      HWGBIZWHJZAKWGWGLZMBUAZIZWKCIZJZAKZWLCNZWHABOWJWOAWIWMWHWNWGPIZWIWMUBATZW
      GBPQRWGWGCUCUDUEWPWQWPDFZWLIZWTCIZJZDKZWQWPWOAPHZAPHZXDXFXEWPWOAPUFWOAUGU
      HXFXCDPPUIZHZXDXFWGEFZLZWLIZXJCIZJZEPHZAPHXHXEXNAPXEWRWOJZAKXNWOAPOXOXNAW
      RXOXNJWSWRXOWOXNWRWOUJWOXMEPXIPIZXKWOXLXPXKXJMIZWIUMWOXLJZWGXIMBPUKXQWIXR
      XQWGXIMGZWIXRJZWGXIMUCXSAEULZXTWGXIETUNWIWOYAXLWIWOYAXLJZJZWIWIWMYCWGBBQW
      MWOWNYBWMWNUJYAWNXLYAWKXJCWGXIWGUOUPUQVEURUSUTSVAVBURUTVCVERVDSVFXCXMDAEP
      PWTXJVGXAXKXBXLWTXJWLVHWTXJCVHVIVJVKXHWTXGIZXCJZDKXDXCDXGOYEXCDXAYDXAUMYE
      XBXAYDWLXGWTWLVLWLXGNMBVMWLVNVOVPVQYDXAXBVRVSVTSWAVADWLCWBVKWQWOAWLCWKWCW
      DWEWF $.
      $( [15-Jan-2012] $)
  $}

  ${
    $d R x $.
    $( The converse of a reflexive class is reflexive.  (Contributed by FL,
       31-Jul-2009.) $)
    cnvref $p |- ( A. x e. ( dom R u. ran R ) x R x <->
    A. x e. ( dom `' R u. ran `' R ) x `' R x ) $=
      ( cv wbr cdm crn cun wral ccnv wb wa brcnvg bicomd anidms ralbiia unidmrn
      wcel cuni eqcomi wrel wceq relcnv relfld ax-mp eqtri raleqi bitri ) ACZUH
      BDZABEBFGZHUHUHBIZDZAUJHULAUKEUKFGZHUIULAUJUHUJQZUIULJUNUNKULUIUHUHUJUJBL
      MNOULAUJUMUJUKRRZUMUOUJBPSUKTUOUMUABUBUKUCUDUEUFUG $.
      $( [31-Jul-2009] $)
  $}

  ${
    $d R x $.
    $( The converse of a reflexive relation is reflexive.  (Contributed by FL,
       19-Sep-2011.) $)
    cnvref2 $p |- ( Rel R -> ( A. x e. U. U. R x R x <->
    A. x e. U. U. `' R x `' R x ) ) $=
      ( wrel cv wbr cuni wral cdm crn cun ccnv relfld raleqdv cnvref a1i relcnv
      wb wceq eqcomd syl 3bitrd ) BCZADZUCBEZABFFZGUDABHBIJZGZUCUCBKZEZAUHHUHIJ
      ZGZUIAUHFFZGUBUDAUEUFBLMUGUKQUBABNOUBUIAUJULUBUHCZUJULRUMUBBPOUMULUJUHLST
      MUA $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d R y $.  $d x y $.
    $( Strong reflexivity implies weak reflexivity.  (Strong and weak
       reflexivity is the difference between a toset and a poset).
       (Contributed by FL, 29-Dec-2011.) $)
    srefwref $p |- ( A. x e. ( dom R u. ran R ) A. y e. ( dom R u. ran R )
   ( x R y \/ y R x ) -> A. x e. ( dom R u. ran R ) x R x ) $=
      ( cv wbr wo cdm crn cun wral wcel weq breq2 orbi12d rcla4va pm4.25 sylibr
      wa breq1 ralimiaa ) ADZBDZCEZUBUACEZFZBCGCHIZJZUAUACEZAUFUAUFKUGRUHUHFZUH
      UEUIBUAUFBALUCUHUDUHUBUAUACMUBUAUACSNOUHPQT $.
      $( [29-Dec-2011] $)
  $}


  $( Equality theorem for functions.  (Contributed by FL, 16-Nov-2008.) $)
  feq123 $p |- ( ( F = G /\ A = C /\ B = D ) ->
    ( F : A --> B <-> G : C --> D ) ) $=
    ( wceq w3a wf wb feq1 3ad2ant1 feq23 3adant1 bitrd ) EFGZACGZBDGZHABEIZABFI
    ZCDFIZPQSTJRABEFKLQRTUAJPABCDFMNO $.
    $( [16-Nov-2008] $)

  $( A class whose union is finite is finite.  (Contributed by FL,
     22-Dec-2008.) $)
  unfinsef $p |- ( U. A e. Fin -> A e. Fin ) $=
    ( cuni cfn wcel cpw pwfi wss pwuni ssfi mpan2 sylbi ) ABZCDLEZCDZACDZLFNAMG
    OAHMAIJK $.
    $( [22-Dec-2008] $)

  $( If the domain of a bijection is finite its range is finite and
     reciprocally.  (Contributed by FL, 31-Jul-2009.) $)
  f1ofi $p |- ( F : A -1-1-onto-> B -> ( A e. Fin <-> B e. Fin ) ) $=
    ( wf1o cfn wcel wfo f1ofo fofi ex syl5com ccnv wi f1ocnv expcom 3syl impbid
    ) ABCDZAEFZBEFZRABCGZSTABCHSUATABCIJKRBACLZDBAUBGZTSMABCNBAUBHTUCSBAUBIOPQ
    $.
    $( [31-Jul-2009] $)

  ${
    $d A B F x $.
    f1fi.1 $e |- F e. C $.
    $( If a 1-to-1 function has a finite codomain its domain is finite.
       (Contributed by FL, 31-Jul-2009.) $)
    f1fi $p |- ( ( B e. Fin /\ F : A -1-1-> B ) -> A e. Fin ) $=
      ( vx wf1 cfn wcel cv wf1o wss wa wex wi elexi f11o ssfi f1ofi syl5ibrcom
      ex com13 imp exlimiv sylbi impcom ) ABDGZBHIZAHIZUGAFJZDKZUJBLZMZFNUHUIOZ
      FABDDCEPQUMUNFUKULUNUHULUKUIUHULUKUIOUHULMUIUKUJHIBUJRAUJDSTUAUBUCUDUEUF
      $.
      $( [31-Jul-2009] $)
  $}

  $( Field of a square cross product.  (Contributed by FL, 10-Oct-2009.) $)
  fldsqcp2 $p |- U. U. ( A X. A ) = A $=
    ( c0 wceq cxp cuni xpeq1 xp0r syl6eq wi unieq unieqd uni0 unieqi eqtri eqtr
    wa expcom eqcoms syl5com wne sylancl mpcom df-ne xpnz cun unixp unidm sylbi
    wn sylancbr pm2.61i ) ABCZAADZEZEZACZUMBCZULUPULUMBADBABAFAGHUQUOBEZEZCZUSB
    CZULUPIUQUNURUMBJKUSURBURBLMLNUTVAPUOBCZULUPUOUSBOVBUPIBAVBBACUPUOBAOQRSUAU
    BULUIABTZVCUPABUCZVDVCVCPUMBTZUPAAUDVEUOAAUEAAAUFAUGHUHUJUK $.
    $( [10-Oct-2009] $)

  ${
    $d A x y z $.
    $( A square cross product ` ( A X. A ) ` is a transitive relation.
       (Contributed by FL, 31-Jul-2009.) $)
    cpref $p |- ( ( A X. A ) o. ( A X. A ) ) C_ ( A X. A ) $=
      ( vx vy vz cxp ccom wss cv wbr wa wal wcel vex brxp biimpri expcom adantl
      wi sylbi com12 adantr imp ax-gen gen2 cotr mpbir ) AAEZUGFUGGBHZCHZUGIZUI
      DHZUGIZJUHUKUGIZRZDKZCKBKUOBCUNDUJULUMUJUHALZUIALZJULUMRZUHUIAACMNUPURUQU
      LUPUMULUQUKALZJUPUMRZUIUKAADMZNUSUTUQUPUSUMUMUPUSJUHUKAAVANOPQSTUASUBUCUD
      BCDUGUEUF $.
      $( [31-Jul-2009] $)
  $}

  $( A square cross product ` ( A X. A ) ` is reflexive.  (Contributed by FL,
     31-Jul-2009.) $)
  scprefat $p |- ( _I |` U. U. ( A X. A ) ) C_ ( A X. A ) $=
    ( cxp cuni wceq cid cres wss fldsqcp2 reseq2 wf1o f1oi f1of fssxp syl ax-mp
    wf sseq1 mpbiri ) AABZCCZADZETFZSGZAHUAUBEAFZDZUCTAEIUEUCUDSGZAAUDJZUFAKUGA
    AUDPUFAAUDLAAUDMNOUBUDSQRNO $.
    $( [31-Jul-2009] $)

  $( Todo: combine scprefat and scprefat2. $)
  $( A square cross product ` ( A X. A ) ` is reflexive.  (Contributed by FL,
     31-Jul-2009.) $)
  scprefat2 $p |- ( _I |` A ) C_ ( A X. A ) $=
    ( cxp cuni wceq cid cres fldsqcp2 reseq2 scprefat sseq1 mpbiri eqcoms ax-mp
    wss syl ) AABZCCZADZEAFZPNZAGREQFZSDTQAEHTSUASUADTUAPNAISUAPJKLOM $.
    $( [31-Jul-2009] $)

  $( A square cross product is symmetric.  (Contributed by FL, 31-Jul-2009.) $)
  sqpsym $p |- `' ( A X. A ) C_ ( A X. A ) $=
    ( cxp ccnv cnvxp eqimssi ) AABZCFAADE $.
    $( [31-Jul-2009] $)

  $( A square cross product is an equivalence relation (in general it's not a
     poset).  (Contributed by FL, 31-Jul-2009.) $)
  sqpeq $p |- Er ( A X. A ) $=
    ( cxp wer ccnv ccom cun wceq cnvxp cpref uneq1 unss2 wi unidm wa eqtr sseq2
    wss biimpd syl mpan2 syl2im mp2 df-er mpbir ) AABZCUEDZUEUEEZFZUEQZUFUEGZUG
    UEQZUIAAHAIUJUFUEFZUEUEFZGZUKUHULQZUIUFUEUEJUGUEUFKUNUMUEGZUOUILZUEMUNUPNUL
    UEGZUQULUMUEOURUOUIULUEUHPRSTUAUBUEUCUD $.
    $( [31-Jul-2009] $)

  ${
    $d R x y z $.  $d S x y z $.
    $( The intersection of two transitive classes is transitive.  (Contributed
       by FL, 31-Jul-2009.) $)
    inttr $p |- ( ( ( R o. R ) C_ R /\ ( S o. S ) C_ S )
      -> ( ( R i^i S ) o. ( R i^i S ) ) C_ ( R i^i S ) ) $=
      ( vx vy vz ccom wss wa cv cin wbr wal cotr brin simpr simpl com12 alanimi
      wi sylbi anim12d an4s syl2anb syl6ibr ex imp sylibr ) AAFAGZBBFBGZHCIZDIZ
      ABJZKZUKEIZULKZHZUJUNULKZSZELZDLZCLZULULFULGUHUIVAUHUJUKAKZUKUNAKZHZUJUNA
      KZSZELZDLZCLZUIVASCDEAMUIVIVAUIUJUKBKZUKUNBKZHZUJUNBKZSZELZDLZCLZVIVASCDE
      BMVQVIVAVPVHUTCVOVGUSDVNVFUREVNVFHZUPVEVMHZUQUPVRVSUMVBVJHVCVKHVRVSSZUOUJ
      UKABNUKUNABNVBVCVJVKVTVRVDVLHVSVRVDVEVLVMVNVFOVNVFPUAQUBUCQUJUNABNUDRRRUE
      TQTUFCDEULMUG $.
      $( [31-Jul-2009] $)
  $}

  $( The relation induced by a transitive relation on a part of its field is
     transitive.  (Taking the intersection of a relation with a square cross
     product is a way to restrict it to a subset of its field.)  (Contributed
     by FL, 31-Jul-2009.) $)
  tricptr $p |- ( ( R o. R ) C_ R ->
  ( ( R i^i ( A X. A ) ) o. ( R i^i ( A X. A ) ) ) C_ ( R i^i ( A X. A ) ) ) $=
    ( ccom wss cxp cin cpref inttr mpan2 ) BBCBDAAEZJCJDBJFZKCKDAGBJHI $.
    $( [31-Jul-2009] $)

  ${
    $d A x $.  $d B x $.
    $( If an infinite set ` A ` is included in the underlying set of a finite
       cover ` B ` then there exists a set of the cover that contains an
       infinite number of element of ` A ` .  (Contributed by FL,
       2-Aug-2009.) $)
    isunscov $p |- ( ( -. A e. Fin /\ B e. Fin /\ A C_ U. B ) ->
    E. x e. B -. ( A i^i x ) e. Fin ) $=
      ( cfn wcel wn cuni wss w3a cv cin wrex wral dfral2 wi wa ciun sylbi com12
      eleq1i iunfi iunin2 uniiun eqcomi ineq2i df-ss eleq1 pm2.24 syl6bi syl ex
      wceq com24 3imp syl5bir pm2.18d ) BDEZFZCDEZBCGZHZIZBAJZKZDEZFACLZVFFVEAC
      MZVBVFVEACNURUSVAVGVFOZUSURVAVHOUSVGVAURVFUSVGVAURVFOZOZUSVGPACVDQZDEZVJA
      CVDUAVLBACVCQZKZDEZVJVKVNDACBVCUBTVOBUTKZDEZVJVNVPDVMUTBUTVMACUCUDUETVAVQ
      VIVAVPBULZVQVIOBUTUFVRVQUQVIVPBDUGUQVFUHUIRSRRUJUKUMSUNUOUP $.
      $( [2-Aug-2009] $)
  $}


  ${
    $d A f x $.
    $( ~ ac5 with the premisse transformed into an antecedent.  (Contributed by
       FL, 2-Aug-2009.) $)
    ac5g $p |- ( A e. _V ->
      E. f ( f Fn A /\ A. x e. A ( x =/= (/) -> ( f ` x ) e. x ) ) ) $=
      ( cvv wcel cv wfn c0 wne cfv wi wral wa wex cif wceq fneq2 anbi12d exbidv
      raleq 0ex elimel ac5 dedth ) BDEZCFZBGZAFZHIUHUFJUHEKZABLZMZCNUFUEBHOZGZU
      IAULLZMZCNBHBULPZUKUOCUPUGUMUJUNBULUFQUIABULTRSAULCBHDUAUBUCUD $.
      $( [2-Aug-2009] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $d F x $.  $d H x $.  $d K x $.
    $( An injection is left-cancelable.  (Contributed by FL, 2-Aug-2009.) $)
    njtlc $p |- ( ( F : B -1-1-> C /\ H : A --> B /\ K : A --> B ) ->
      ( ( F o. H ) = ( F o. K ) -> H = K ) ) $=
      ( vx wf wceq ccom cfv wral wa wcel syl fvco3 syl3anc wi wfn ffn wfun ffun
      wf1 w3a cv f1f 3ad2ant1 adantr simpl2 simpr simpl3 eqeq12d simpl1 ffvelrn
      3ad2antl2 3ad2antl3 f1fveq biimpd syl12anc sylbid ralimdva anim2d wb 3syl
      fnfco ex imp 3adant3 3adant2 eqfnfv syl2anc anim12i 3adant1 3imtr4d ) BCD
      UCZABEHZABFHZUDZAAIZGUEZDEJZKZVTDFJZKZIZGALZMZVSVTEKZVTFKZIZGALZMZWAWCIZE
      FIZVRWFWKVSVRWEWJGAVRVTANZMZWEWHDKZWIDKZIZWJWPWBWQWDWRWPDUAZVPWOWBWQIVRWT
      WOVOVPWTVQVOBCDHZWTBCDUFZBCDUBOUGUHZVOVPVQWOUIVRWOUJZABVTDEPQWPWTVQWOWDWR
      IXCVOVPVQWOUKXDABVTDFPQULWPVOWHBNZWIBNZWSWJRVOVPVQWOUMVPVOWOXEVQABVTEUNUO
      VQVOWOXFVPABVTFUNUPVOXEXFMMWSWJBCWHWIDUQURUSUTVAVBVRWAASZWCASZWMWGVCVOVPX
      GVQVOVPXGVOXADBSZVPXGRXBBCDTZXIVPXGBADEVEVFVDVGVHVOVQXHVPVOVQXHVOXAXIVQXH
      RXBXJXIVQXHBADFVEVFVDVGVIGAAWAWCVJVKVREASZFASZMZWNWLVCVPVQXMVOVPXKVQXLABE
      TABFTVLVMGAAEFVJOVN $.
      $( [2-Aug-2009] $)
  $}

  ${
    $d A y $.  $d B y $.  $d F x y $.  $d H x y $.  $d K x y $.
    $( A surjection is right-cancelable.  (Contributed by FL, 21-Nov-2011.) $)
    surrc2 $p |- ( ( F : A -onto-> B /\ H Fn B /\ K Fn B ) ->
      ( ( H o. F ) = ( K o. F ) <-> H = K ) ) $=
      ( vx vy wfo wfn ccom wceq wa cdm cfv adantr wi impcom imp syl wb w3a wral
      cv fndm eqtr3 syl2an 3adant1 wcel fveq1 wfun fof fnfun fvco3 3exp 3adant3
      wf syl5com 3adant2 eqeq12d biimpd com3r ralrimiv foeq3 eqcoms fveq2 cbvfo
      syl5 ex mpbid jca anim12i eqfunfv mpbird coeq1 impbid1 ) ABCHZDBIZEBIZUAZ
      DCJZECJZKZDEKZVSWBWCVSWBLZWCDMZEMZKZFUCZDNZWHENZKZFWEUBZLZWDWGWLVSWGWBVQV
      RWGVPVQWEBKZWFBKWGVRBDUDZBEUDWEWFBUEUFUGOWDGUCZCNZDNZWQENZKZGAUBZWLWDWTGA
      WBVSWPAUHZWTPZWBWPVTNZWPWANZKZVSXCPWPVTWAUIVSXBXFWTVSXBXFWTPVSXBLZXFWTXGX
      DWRXEWSVSXBXDWRKZVPVQXBXHPZVRVPABCUPZDUJZXIVQABCUKZBDULZXKXJXIXKXJXBXHABW
      PDCUMUNQUFUORVSXBXEWSKZVPVRXBXNPZVQVPVRXOVREUJZVPXOBEULZVPXJXPXOXLXPXJXBX
      NABWPECUMUNUQVGRURRUSUTVHVASQVBWDAWECHZXAWLTVSXRWBVPVQXRVRVQVPXRVQWNVPXRP
      WOWNVPXRVPXRTBWEBWEACVCVDUTSQUOOWTWKGFAWECWQWHKWRWIWSWJWQWHDVEWQWHEVEUSVF
      SVIVJWDXKXPLZWCWMTVSXSWBVQVRXSVPVQXKVRXPXMXQVKUGOFDEVLSVMVHDECVNVO $.
      $( [21-Nov-2011] $)
  $}

  ${
    $d A x y $.
    $( Restriction of the identity to a singleton.  (Contributed by FL,
       2-Aug-2009.) $)
    restidsing $p |- ( _I |` { A } ) = ( { A } X. { A } ) $=
      ( vx vy cid csn cres cxp relres cv wcel wa wceq elsn wi cvv imp jca bitri
      ex sylbi relxp cop wbr df-br bicomi anbi1i simpr ideqg biimpd ax-mp eqtr2
      vex syl6ibr syl syl5bi eqtr3 ideq equcom sylibr com12 simpl impbii opelxp
      opelres 3bitr4i eqrelriiv ) BCDAEZFZVGVGGZDVGHVGVGUABIZCIZUBZDJZVJVGJZKZV
      NVKVGJZKZVLVHJVLVIJVOVJVKDUCZVNKZVQVMVRVNVRVMVJVKDUDUEUFVSVQVSVNVPVRVNUGV
      RVNVPVNVJALZVRVPBAMZVRVJVKLZVTVPNVKOJZVRWBNCULZWCVRWBVJVKOUHUIUJWBVTVKALZ
      VPWBVTWEVJVKAUKSCAMZUMUNUOPQVQVRVNVNVPVRVNVTVPVRNWAVPVTVRVPWEVTVRNWFWEVTV
      RWEVTKVKVJLZVRVKVJAUPVRWBWGVJVKWDUQBCURRUSSTUTTPVNVPVAQVBRVJVKDVGWDVDVJVK
      VGVGWDVCVEVF $.
      $( [2-Aug-2009] $)
  $}

  $( The intersection of the identity function with a square cross product.
     (Contributed by FL, 2-Aug-2009.) $)
  residcp $p |- ( _I i^i ( A X. A ) ) = ( _I |` A ) $=
    ( cid cxp cin cres cvv wss ssid ssv xpss12 mp2an sslin ax-mp sseqtr4i resss
    df-res scprefat2 ssini eqssi ) BAACZDZBAEZUABAFCZDZUBTUCGZUAUDGAAGAFGUEAHAI
    AAAFJKTUCBLMBAPNUBBTBAOAQRS $.
    $( [2-Aug-2009] $)

  $( Image under a restricted class.  (Contributed by FL, 31-Aug-2009.) $)
  imrescl $p |- ( A C_ B -> ( ( C |` B ) " A ) = ( C " A ) ) $=
    ( wss cres cima crn cin wceq df-ima resres rneqi df-ss incom reseq2d reseq2
    a1i rneqd eqcomi 3eqtrd sylbi ) ABDZCBEZAFZUCAEZGZCBAHZEZGZCAFZUDUFIUBUCAJQ
    UFUIIUBUEUHCBAKLQUBABHZAIZUIUJIABMULUICUKEZGCAEZGZUJULUHUMULUGUKCUGUKIULBAN
    QORULUMUNUKACPRUOUJIULUJUOCAJSQTUAT $.
    $( [31-Aug-2009] $)

  ${
    $d R x y $.
    $( Two ways of saying a relation is symmetric.  (Contributed by FL,
       31-Aug-2009.) $)
    twsymr $p |- ( Rel R -> ( R = `' R <-> A. x A. y ( x R y -> y R x ) ) ) $=
      ( ccnv wceq wss wa wrel cv wbr wi wal eqss cnvsym biimpi a1d adantl com12
      dfrel2 cnvss sseq1 syl5ibcom sylbir sylbi imp biimpri jca impbid syl5bb
      ex ) CCDZECUKFZUKCFZGZCHZAIZBIZCJUQUPCJKBLALZCUKMUOUNURUNUOURUMUOURKULUMU
      RUOUMURABCNZOPQRUOURUNUOURGULUMUOURULUOUKDZCEZURULKCSURVAULURUMVAULKUSUMU
      TUKFVAULUKCTUTCUKUAUBUCRUDUEURUMUOUMURUSUFQUGUJUHUI $.
      $( [31-Aug-2009] $)
  $}

  ${
    $d R x y z $.
    $( Projection of the first elements of the pairs of a relation ` R ` .
       (Contributed by FL, 15-Oct-2012.) $)
    prj1b $p |- ( Rel R -> ( 1st " R ) =
    { x | E. y <. x , y >. e. R } ) $=
      ( vz cv c1st wcel cop wb wceq wbr wi c2nd cfv cvv df-br ax-mp vex sylbi
      wa wrel cima wex cab wal wrex cxp wss df-rel ssel elxp6 wfn fo1st fnopfvb
      wfo fofn mp2an eleq1 opeq1 eleq1d biimpcd syl6bi com3r sylbir adantr syli
      com3l com23 com13 fvex opeq2 cla4ev syl8 rexlimiv com12 op1st mpbir breq1
      opex mpbi rcla4ev mpan2 exlimiv impbid1 elimag abid 3bitr4g alrimiv ax-17
      hbab1 cleqf sylibr ) CUAZAEZFCUBZGZWNWNBEZHZCGZBUCZAUDZGZIZAUEWOXAJWMXCAW
      MDEZWNFKZDCUFZWTWPXBWMXFWTXFWMWTXEWMWTLDCXDCGZXEWMWNXDMNZHZCGZWTWMXEXGXJW
      MCOOUGZUHZXEXGXJLZLCUIXLXGXEXJXGXLXDXKGZXEXJLZCXKXDUJXNXDXDFNZXHHZJZXPOGX
      HOGTZTXGXOLZXDOOUKXRXTXSXEXRXGXJXEXDWNHFGZXRXMLZXDWNFPYAXPWNJZYBFOULZXDOG
      YCYAIOOFUOYDUMOOFUPQZDROXDWNFARZUNUQXRXGYCXJXRXGXQCGZYCXJLXDXQCURYCYGXJYC
      XQXICXPWNXHUSUTVAVBVCVDSVGVESVFVHSVIWSXJBXHXDMVJWQXHJWRXICWQXHWNVKUTVLVMV
      NVOWSXFBWSWRWNFKZXFYHWRWNHFGZWRFNWNJZYIWNWQYFVPYDWROGYJYIIYEWNWQVSOWRWNFY
      FUNUQVTWRWNFPVQXEYHDWRCXDWRWNFVRWAWBWCWDWNOGWPXFIYFDWNFCOWEQWTAWFWGWHADWO
      XAXDWOGAWIWTADWJWKWL $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d R x y z $.
    $( Use fnopfvb instead of fnopovb. $)
    $( Projection of the second elements of the pairs of a relation ` R ` .
       (Contributed by FL, 15-Oct-2012.) $)
    prj3 $p |- ( Rel R -> ( 2nd " R ) = { y | E. x <. x , y >. e. R } ) $=
      ( vz cv c2nd wcel cop wb wceq wbr wi c1st cfv cvv df-br ax-mp vex sylbi
      wa wrel cima wex cab wal wrex cxp wss df-rel ssel elxp6 wfn fo2nd fnopfvb
      wfo fofn mp2an eleq1 opeq2 eleq1d biimpcd syl6bi com3r sylbir adantr syli
      com3l com23 com13 fvex opeq1 cla4ev syl8 rexlimiv com12 op2nd mpbir breq1
      opex mpbi rcla4ev mpan2 exlimiv impbid1 elimag abid 3bitr4g alrimiv ax-17
      hbab1 cleqf sylibr ) CUAZBEZFCUBZGZWNAEZWNHZCGZAUCZBUDZGZIZBUEWOXAJWMXCBW
      MDEZWNFKZDCUFZWTWPXBWMXFWTXFWMWTXEWMWTLDCXDCGZXEWMXDMNZWNHZCGZWTWMXEXGXJW
      MCOOUGZUHZXEXGXJLZLCUIXLXGXEXJXGXLXDXKGZXEXJLZCXKXDUJXNXDXHXDFNZHZJZXHOGX
      POGTZTXGXOLZXDOOUKXRXTXSXEXRXGXJXEXDWNHFGZXRXMLZXDWNFPYAXPWNJZYBFOULZXDOG
      YCYAIOOFUOYDUMOOFUPQZDROXDWNFBRZUNUQXRXGYCXJXRXGXQCGZYCXJLXDXQCURYCYGXJYC
      XQXICXPWNXHUSUTVAVBVCVDSVGVESVFVHSVIWSXJAXHXDMVJWQXHJWRXICWQXHWNVKUTVLVMV
      NVOWSXFAWSWRWNFKZXFYHWRWNHFGZWRFNWNJZYIWQWNARYFVPYDWROGYJYIIYEWQWNVSOWRWN
      FYFUNUQVTWRWNFPVQXEYHDWRCXDWRWNFVRWAWBWCWDWNOGWPXFIYFDWNFCOWEQWTBWFWGWHBD
      WOXAXDWOGBWIWTBDWJWKWL $.
      $( [15-Oct-2012] $)
  $}


  ${
    $d A x $.
    $( The image under ` 1st ` of a class with no pairs inside.  (Contributed
       by FL, 31-Aug-2009.) $)
    imfstnrelc $p |- ( ( ( A i^i ( _V X. _V ) ) = (/) /\ A =/= (/) )
   -> ( 1st " A ) = { (/) } ) $=
      ( vx cvv cxp cin c0 wceq wne wa c1st cima cv csn ciun cdm wss fo1st ax-mp
      syl wcel cfv wfun wfo ssv wfn fofn fndm sseqtr4i pm3.2i a1i dfimafn2 cuni
      fofun 1stval wn wi wral wal biimpi df-ral sylib 19.21bi adantr imp dmsnn0
      disj bicomi necon1bbii 0ss sseq1 mpbiri sylbi uni0b sylibr sneqd iuneq2dv
      syl5eq iunconst adantl 3eqtrd ) ACCDZEFGZAFHZIZJAKZBABLZJUAZMZNZBAFMZNZWJ
      WDJUBZAJOZPZIZWEWIGWOWDWLWNCCJUCZWLQCCJUMRACWMAUDJCUEZWMCGWPWQQCCJUFRCJUG
      RUHUIUJBAJUKSWDBAWHWJWDWFATZIZWGFWSWGWFMOZULZFWFUNWSWTWJPZXAFGWSWFWATZUOZ
      XBWDWRXDWBWRXDUPZWCWBXEBWBXDBAUQZXEBURWBXFBAWAVFUSXDBAUTVAVBVCVDXDWTFGZXB
      XCWTFXCWTFHWFVEVGVHXGXBFWJPWJVIWTFWJVJVKVLSWTVMVNVQVOVPWCWKWJGWBBAWJVRVSV
      T $.
      $( [31-Aug-2009] $)
  $}

  ${
    $d R x y $.
    $( The projection of the first elements of the pairs of a relation ` R ` is
       its domain.  (Contributed by FL, 5-Oct-2009.) $)
    prjdmn $p |- ( Rel R -> ( 1st " R ) = dom R ) $=
      ( vx vy wrel c1st cima cv cop wcel wex cab cdm prj1b dfdm3 syl6eqr ) ADEA
      FBGCGHAICJBKALBCAMBCANO $.
      $( [5-Oct-2009] $)
  $}

  ${
    $d R x y $.
    $( The projection of the second elements of the pairs of a relation ` R `
       is its range.  (Contributed by FL, 15-Oct-2012.) $)
    prjrn $p |- ( Rel R -> ( 2nd " R ) = ran R ) $=
      ( vx vy wrel c2nd cima cv cop wcel wex cab crn prj3 dfrn3 syl6eqr ) ADEAF
      BGCGHAIBJCKALBCAMBCANO $.
      $( [15-Oct-2012] $)
  $}

  $( Projection of a cross product.  (Contributed by FL, 5-Oct-2009.) $)
  prjcp1 $p |- ( B =/= (/) -> ( 1st " ( A X. B ) ) = A ) $=
    ( c0 wne c1st cxp cima cdm wrel wceq wi relxp prjdmn a1d ax-mp dmxp eqtrd )
    BCDZEABFZGZSHZASIZRTUAJZKABLUBUCRSMNOABPQ $.
    $( [5-Oct-2009] $)

  $( Projection of a cross product.  (Contributed by FL, 15-Oct-2012.) $)
  prjcp2 $p |- ( A =/= (/) -> ( 2nd " ( A X. B ) ) = B ) $=
    ( c0 wne c2nd cxp cima crn wrel wceq wi relxp prjrn a1d ax-mp rnxp eqtrd )
    ACDZEABFZGZSHZBSIZRTUAJZKABLUBUCRSMNOABPQ $.
    $( [15-Oct-2012] $)

  ${
    $d A v w x y z $.  $d ph x $.  $d ta v w y z $.
    eloi.1 $e |- ( y = ( 1st ` ( 1st ` A ) ) -> ( ph <-> ps ) ) $.
    eloi.2 $e |- ( z = ( 2nd ` ( 1st ` A ) ) -> ( ps <-> ch ) ) $.
    eloi.3 $e |- ( v = ( 1st ` ( 2nd ` A ) ) -> ( ch <-> th ) ) $.
    eloi.4 $e |- ( w = ( 2nd ` ( 2nd ` A ) ) -> ( th <-> ta ) ) $.
    $( A consequence of membership in a class abstraction whose elements belong
       to ` ( ( _V X. _V ) X. ( _V X. _V ) ) ` using ordered pair extractors.
       (Used by category theory).  (Contributed by FL, 24-Sep-2007.) $)
    eloi $p |- ( A e. { x | E. y E. z E. v E. w
      ( x = <. <. y , z >. , <. v , w >. >. /\ ph ) } -> ta ) $=
      ( cop wex wcel cfv cvv cv wceq wa cab c1st eqeq1 anbi1d 4exbidv elabg ibi
      c2nd cxp id opex opelxpi mp2an syl6eqel wrel relxp 1st2nd syl fveq2 op1st
      mpan syl6eq vex op2nd opeq12d eqtrd adantr exlimivv eleq1 wb fvex 3pm3.2i
      w3a elo syl6bb biimpcd mpd ) KFUAZGUAZHUAZPZJUAZIUAZPZPZUBZAUCZIQJQHQGQZF
      UDZRZKKUESZUESZWNUKSZPZKUKSZUESZWRUKSZPZPZUBZEWMKWHUBZAUCZIQJQZHQGQZXCWMX
      GWKXGFKWLWAKUBZWJXEGHJIXHWIXDAWAKWHUFUGUHUIUJXFXCGHXEXCJIXDXCAXDKWNWRPZXB
      XDKTTULZRZKXIUBZXDKWHXJXDUMWDTRWGTRWHXJRWBWCUNZWEWFUNZWDWGTTUOUPUQXJURZXK
      XLTTUSZKXJUTVDVAXDWNWQWRXAXDWNXJRZWNWQUBZXDWNWDXJXDWNWHUESWDKWHUEVBWDWGXM
      VCVEWBTRWCTRWDXJRGVFHVFWBWCTTUOUPUQXOXQXRXPWNXJUTVDVAXDWRXJRZWRXAUBZXDWRW
      GXJXDWRWHUKSWGKWHUKVBWDWGXMXNVGVEWETRWFTRWGXJRJVFIVFWEWFTTUOUPUQXOXSXTXPW
      RXJUTVDVAVHVIVJVKVKVAXCWMEXCWMXBWLRZEKXBWLVLWOTRZWPTRZWSTRZVPWTTRYAEVMYBY
      CYDWNUEVNWNUKVNWRUEVNVOWRUKVNABCDEFGHIJWOWPWSWTTTTTLMNOVQUPVRVSVT $.
      $( [24-Sep-2007] $)
  $}

  $c t+ $.
  $c t* $.

  $( Extend class notation to include the transitive closure symbol. $)
  ctcl $a class t+ $.

  $( Extend class notation with transitive closure. $)
  crtcl $a class t* $. $( Extend class notation with reflexive transitive
    closure. $)

  ${
    $d x y z $.
    $( Transitive closure of a relation.  Experimental. $)
    df-trcl $a |- t+ = { <. x , y >. |
      y = |^| { z | ( x C_ z /\ ( z o. z ) C_ z ) } } $.

    $( Reflexive-transitive closure of a relation.  Experimental. $)
    df-rtrcl $a |- t* = { <. x , y >. |
      y = |^| { z | ( ( _I |` ( dom x u. ran x ) ) C_ z /\ x C_ z
      /\ ( z o. z ) C_ z ) } } $.
  $}


  $( The double union of an intersection is a part of the intersections of the
     unions.  (Contributed by FL, 24-Jan-2010.) $)
  uuniin $p |- U. U. ( A i^i B ) C_ ( U. U. A i^i U. U. B ) $=
    ( cin cuni wss uniin uniss sstr2 mpi syl ax-mp ) ABCDZADZBDZCZEZLDZMDNDCZEZ
    ABFPQODZEZSLOGUATRESMNFQTRHIJK $.
    $( [24-Jan-2010] $)


  $( Building a set with two elements.  (Contributed by FL, 11-Aug-2008.) $)
  set2elt $p |- ( ( C ~~ 2o /\ A e. C /\ B e. C )
     -> ( A =/= B -> C = { A , B } ) ) $=
    ( c2o cen wbr wcel wne cpr wceq wi wss prssg w3a cfn com 2onn syl syl5com
    wa pr2nelem ssid ssnnfi mp2an enfi mpan mpbiri ensymg ax-mp entr prex ensym
    wb fisseneq eqcomd 3exp com3r com13 mpcom 3expia com24 sylbid pm2.43i com12
    ex 3impib ) CDEFZACGZBCGZABHZCABIZJZKZVHVITZVGVMVNVGVMKZVNVNVKCLZVOABCCCMVN
    VJVGVPVLVHVIVJVGVPVLKZKVHVIVJNVKDEFZVGVQABCCUACOGZVGVRVQKVGVSDOGZDPGZDDLVTQ
    DUBDDUCUDWAVGVSVTUMQCDPUEUFUGVRVGVSVQVRDVKEFZVGVSVQKZWAVRWBKQVKDPUHUIVGWBWC
    VGWBTCVKEFZWCCDVKUJWDVKCEFZWCCVKABUKULVSVPWEVLVSVPWEVLVSVPWENVKCVKCUNUOUPUQ
    RRVESURUSSUTVAVBVCVDVF $.
    $( [11-Aug-2008] $)


  ${
    oveq123i.1 $e |- A = C $.
    oveq123i.2 $e |- B = D $.
    oveq123i.3 $e |- F = G $.
    $( Equality inference for operation value.  (Contributed by FL,
       11-Jul-2010.) $)
    oveq123i $p |- ( A F B ) = ( C G D ) $=
      ( co oveq12i oveqi eqtri ) ABEJCDEJCDFJACBDEGHKEFCDILM $.
      $( [11-Jul-2010] $)
  $}

  $( A class builder defined by a negation.  (Contributed by FL,
     18-Sep-2010.) $)
  clsbldneg $p |- { x | -. ph } = ( _V \ { x | ph } ) $=
    ( wn cvv crab cv wcel wa cab cdif df-rab rabab difab difeq1i eqtr3i 3eqtr3i
    abid2 ) ACZBDEBFDGZRHBIZRBIDABIZJZRBDKRBLSBIZUAJTUBSABMUCDUABDQNOP $.
    $( [18-Sep-2010] $)


  $( A class builder defined by an implication.  (Contributed by FL,
     18-Sep-2010.) $)
  clsbldimp $p |- { x | ( ph -> ps ) } =
                  ( { x | -. ph } u. { x | ps } ) $=
    ( wi cab wn wo cun imor abbii unab eqtr4i ) ABDZCEAFZBGZCENCEBCEHMOCABIJNBC
    KL $.
    $( [18-Sep-2010] $)


  $( A mapping is a member of the powerset of the cross product of its domain
     and codomain.  (Contributed by FL, 30-Dec-2010.) $)
  mappow $p |- ( ( A e. R /\ B e. S ) ->
    ( F : A --> B -> F e. ~P ( A X. B ) ) ) $=
    ( wcel wa wf cmap co cxp cpw elmapg ancoms wss mapsspw adantr sseld sylbird
    wb ) ACFZBDFZGZABEHZEBAIJZFZEABKLZFUBUAUFUDTBAEDCMNUCUEUGEUAUEUGOUBBACPQRS
    $.
    $( [30-Dec-2010] $)

  ${
    $d A x y $.  $d ph y $.
    $( Membership in the intersection of a class abstraction.  (Contributed by
       FL, 30-Dec-2010.) $)
    elintabg $p |- ( A e. V ->
      ( A e. |^| { x | ph } <-> A. x ( ph -> A e. x ) ) ) $=
      ( vy cv cab cint wcel wi wal wceq eleq1 imbi2d albidv bibi12d vex elintab
      wb vtoclg ) EFZABGHZIZAUABFZIZJZBKZSCUBIZACUDIZJZBKZSECDUACLZUCUHUGUKUACU
      BMULUFUJBULUEUIAUACUDMNOPABUAEQRT $.
      $( [30-Dec-2010] $)
  $}

  $( A singleton of a set belongs to the power class of a class containing the
     set.  (Contributed by FL, 30-Dec-2010.) $)
  snelpwg $p |- ( A e. V -> ( A e. B <-> { A } e. ~P B ) ) $=
    ( wcel cvv csn cpw wb elex c0 cif wceq eleq1 sneq eleq1d bibi12d 0ex elimel
    snelpw dedth syl ) ACDAEDZABDZAFZBGZDZHZACIUBUGUBAJKZBDZUHFZUEDZHAJAUHLZUCU
    IUFUKAUHBMULUDUJUEAUHNOPUHBAJEQRSTUA $.
    $( [30-Dec-2010] $)

  ${
    $d A x y $.  $d F z $.  $d x y z $.
    dff1o6f.1 $e |- ( z e. F -> A. x z e. F ) $.
    dff1o6f.2 $e |- ( z e. F -> A. y z e. F ) $.
    $( A one-to-one onto function in terms of function values.  (Contributed by
       FL, 1-Jan-2011.) $)
    dff1o6f $p |- ( F : A -1-1-onto-> B <-> ( F Fn A /\ ran F = B /\
             A. x e. A A. y e. A ( ( F ` x ) = ( F ` y ) -> x = y ) ) ) $=
      ( wf1o wf1 wfo wa wf cv cfv wceq wi wral wfn crn w3a df-f1o df-fo anbi12i
      dff13f df-3an wss eqimss anim2i df-f sylibr pm4.71ri anbi1i an32 3bitrri
      3bitri ) DEFIDEFJZDEFKZLDEFMZANZFOBNZFOPUTVAPQBDRADRZLZFDSZFTZEPZLZLZVDVF
      VBUAZDEFUBUQVCURVGABCDEFGHUEDEFUCUDVIVGVBLUSVGLZVBLVHVDVFVBUFVGVJVBVGUSVG
      VDVEEUGZLUSVFVKVDVEEUHUIDEFUJUKULUMUSVGVBUNUOUP $.
      $( [1-Jan-2011] $)
  $}

  $( The non-empty components of a finite cross product are finite.
     (Contributed by FL, 22-Feb-2011.) $)
  fixpb $p |- ( ( A =/= (/) /\ B =/= (/) ) ->
    ( ( A X. B ) e. Fin <-> ( A e. Fin /\ B e. Fin ) ) ) $=
    ( c0 wne wa cxp cfn wcel wi xpnz xpfir expcom sylbi xpfi impbid1 ) ACDBCDEZ
    ABFZGHZAGHBGHEZPQCDZRSIABJRTSABKLMABNO $.
    $( [22-Feb-2011] $)

  $( A cross product is finite iff one of its components is empty or both its
     components are finite.  (Contributed by FL, 22-Feb-2011.) $)
  fixpc $p |- ( ( A X. B ) e. Fin <->
       ( A = (/) \/ B = (/) \/ ( A e. Fin /\ B e. Fin ) ) ) $=
    ( cxp cfn wcel c0 wceq wa w3o wo wn wne wi neanior fixpb biimpd sylbir 0fin
    eqeltri syl6eqel com12 orrd df-3or sylibr xpeq1 xp0r xpeq2 xp0 3jaoi impbii
    xpfi ) ABCZDEZAFGZBFGZADEBDEHZIZUMUNUOJZUPJUQUMURUPURKZUMUPUSAFLBFLHZUMUPMA
    FBFNUTUMUPABOPQUAUBUNUOUPUCUDUNUMUOUPUNULFBCZDAFBUEVAFDBUFRSTUOULAFCZDBFAUG
    VBFDAUHRSTABUKUIUJ $.
    $( [22-Feb-2011] $)

  $( The power set of a set is equinumerous to set exponentiation with a base
     of ordinal ` 2o ` .  (Contributed by FL, 22-Feb-2011.) $)
  pw2eng $p |- ( A e. V -> ~P A ~~ ( 2o ^m A ) ) $=
    ( wcel cvv cpw c2o cmap co cen wbr elex c0 cif wceq pweq breq12d 0ex elimel
    oveq2 pw2en dedth syl ) ABCADCZAEZFAGHZIJZABKUCUFUCALMZEZFUGGHZIJALAUGNUDUH
    UEUIIAUGOAUGFGSPUGALDQRTUAUB $.
    $( [22-Feb-2011] $)

  ${
    $d x y $.
    $( The class of natural numbers is the intersection of the limits
       ordinals.  (Contributed by FL, 22-Feb-2011.) $)
    omslim $p |- om = ( On i^i |^| { x | Lim x } ) $=
      ( vy com con0 cv wlim cab cint cin word wcel wi wal wa vex bicomi elintab
      elon anbi12i elom elin 3bitr4i eqriv ) BCDAEZFZAGHZIZBEZJZUEUHUDKLAMZNUHD
      KZUHUFKZNUHCKUHUGKUIUKUJULUKUIUHBOZRPULUJUEAUHUMQPSAUHUMTUHDUFUAUBUC $.
      $( [22-Feb-2011] $)

    $( Assuming the axiom of infinity, the class of natural numbers is the
       intersection of all the limit ordinals.  Since ` om ` is a limit ordinal
       it is then the smallest limit ordinal.  (Contributed by FL,
       22-Feb-2011.) $)
    omslim2 $p |- om = |^| { x | Lim x } $=
      ( vy com cv wlim cab cint wcel wi wal abid dfom4 eleq2i vex elintab eqriv
      3bitr4i ) BCADZEZAFGZBDZSUARHIAJZBFZHUBUACHUATHUBBKCUCUABALMSAUABNOQP $.
      $( [22-Feb-2011] $)
  $}

  $( Absorption law for multiplication with an infinite cardinal.  Equivalent
     to Proposition 10.41 of [TakeutiZaring] p. 95.  (Contributed by FL,
     22-Feb-2011.) $)
  infxpg $p |- ( ( om ~<_ A /\ B =/= (/) /\ ( A e. C /\ B e. D ) ) ->
                   ( A X. B ) ~~ ( A u. B ) ) $=
    ( com cdom wbr c0 wne wcel cxp cun cen cvv wi elex cif wceq breq12d imbi2d
    wa breq2 xpeq1 uneq1 imbi12d neeq1 xpeq2 uneq2 omex elimel infxp ex dedth2h
    syl2an com3l 3imp ) EAFGZBHIZACJZBDJZUAZABKZABLZMGZVAUQURVDUSANJZBNJZUQURVD
    OZOZUTACPBDPVEVFVHEVEAEQZFGZURVIBKZVIBLZMGZOZOVJVFBEQZHIZVIVOKZVIVOLZMGZOZO
    ABEEAVIRZUQVJVGVNAVIEFUBWAVDVMURWAVBVKVCVLMAVIBUCAVIBUDSTUEBVORZVNVTVJWBURV
    PVMVSBVOHUFWBVKVQVLVRMBVOVIUGBVOVIUHSUETVJVPVSVIVOAENUIUJBENUIUJUKULUMUNUOU
    P $.
    $( [22-Feb-2011] $)

  $( An infinite set strictly dominates a natural number.  (Contributed by FL,
     22-Feb-2011.) $)
  infsdomnng $p |- ( A e. V -> ( ( om ~<_ A /\ B e. om ) -> B ~< A ) ) $=
    ( wcel cvv com cdom wbr wa csdm wi elex cif wceq anbi1d imbi12d omex elimel
    breq2 infsdomnn dedth syl ) ACDAEDZFAGHZBFDZIZBAJHZKZACLUCUHFUCAFMZGHZUEIZB
    UIJHZKAFAUINZUFUKUGULUMUDUJUEAUIFGSOAUIBJSPUIBAFEQRTUAUB $.
    $( [22-Feb-2011] $)

  $( Any operation can be restricted to ` ( _V X. _V ) ` .  (Contributed by FL,
     20-Mar-2011.) $)
  resid2 $p |- ( ( Rel A /\ Rel dom A ) -> ( A |` ( _V X. _V ) ) = A ) $=
    ( cdm wrel cvv cxp wss cres wceq df-rel relssres sylan2b ) ABZCACLDDEZFAMGA
    HLIAMJK $.
    $( [20-Mar-2011] $)

  $( Image of an element of transitive class ` B ` under a class restricted by
     ` B ` .  (Contributed by FL, 20-Mar-2011.) $)
  imrestr $p |- ( ( Tr B /\ A e. B ) -> ( ( C |` B ) " A ) = ( C " A ) ) $=
    ( wtr wcel cres cima wceq wss trss imrescl syl6 imp ) BDZABEZCBFAGCAGHZNOAB
    IPBAJABCKLM $.
    $( [20-Mar-2011] $)

  $( Image of an element of a ordinal ` B ` under a class restricted by
     ` B ` .  (Contributed by FL, 20-Mar-2011.) $)
  imresord $p |- ( ( Ord B /\ A e. B ) -> ( ( C |` B ) " A ) = ( C " A ) ) $=
    ( word wtr wcel cres cima wceq ordtr imrestr sylan ) BDBEABFCBGAHCAHIBJABCK
    L $.
    $( [20-Mar-2011] $)

  $( If ` A ` is a part of ` B ` and ` B ` a part of ` C ` and ` A ` is
     equipotent to ` C ` then ` A ` is equipotent to ` B ` .  The art of
     sandwich applied to set theory.  (Contributed by FL, 16-Apr-2011.) $)
  sndw $p |- ( ( A C_ B /\ B C_ C /\ C e. _V )
     -> ( A ~~ C -> A ~~ B ) ) $=
    ( wss cvv wcel w3a cen wbr cdom wa relen brrelexi 3ad2ant3 ssdomg syl com4r
    wi 3exp syl6 3imp1 ssdom2g impcom 3adant1 adantr ensymg endom domtr syl2anc
    imp jca ex sbth ) ABDZBCDZCEFZGZACHIZABJIZBAJIZKZABHIUQURVAUQURKZUSUTUNUOUP
    URUSUOUPURUNUSUOUPURUNUSRZUOUPURGAEFZVCURUOVDUPACHLMNABEOPSQUAVBBCJIZCAJIZU
    TUQVEURUOUPVEUNUPUOVEBCEUBUCUDUEUQURVFUQURCAHIZVFUPUNURVGRUOACEUFNCAUGTUJBC
    AUHUIUKULABUMT $.
    $( [16-Apr-2011] $)

  $( If ` A ` is a part of ` B ` and ` B ` a part of ` C ` and ` A ` is
     equipotent to ` C ` then ` A ` is equipotent to ` B ` .  The art of
     sandwich applied to set theory.  (Contributed by FL, 16-Apr-2011.) $)
  sndw2 $p |- ( ( A C_ B /\ B C_ C /\ C e. D )
     -> ( A ~~ C -> A ~~ B ) ) $=
    ( wcel wss cvv cen wbr wi elex sndw syl3an3 ) CDEABFBCFCGEACHIABHIJCDKABCLM
    $.
    $( [16-Apr-2011] $)

  $( If a successor of ` A ` belongs to an ordinal, so does ` A ` .
     (Contributed by FL, 20-Mar-2011.) $)
  ordsuccl $p |- ( ( Ord B /\ suc A e. B ) -> A e. B ) $=
    ( word wtr csuc wcel ordtr trsuc sylan ) BCBDAEBFABFBGBAHI $.
    $( [20-Mar-2011] $)

  $( If a successor of ` A ` belongs to an ordinal, so does ` A ` .
     (Contributed by FL, 20-Mar-2011.) $)
  ordsuccl2 $p |- ( ( B e. On /\ suc A e. B ) -> A e. B ) $=
    ( con0 wcel wtr csuc word eloni ordtr syl trsuc sylan ) BCDZBEZAFBDABDMBGNB
    HBIJBAKL $.
    $( [20-Mar-2011] $)

  $( If a successor of ` A ` belongs to an ordinal, ` A ` is a part of the
     ordinal.  (Contributed by FL, 20-Mar-2011.) $)
  ordsuccl3 $p |- ( ( B e. On /\ suc A e. B ) -> A C_ B ) $=
    ( con0 wcel csuc wa wtr wss word eloni ordtr syl adantr ordsuccl2 trss sylc
    ) BCDZAEBDZFBGZABDABHQSRQBISBJBKLMABNBAOP $.
    $( [20-Mar-2011] $)

  $( Trichotomy law for dominance and strict dominance.  This theorem is
     equivalent to the Axiom of Choice.  (Contributed by FL, 16-Apr-2011.) $)
  domtri2 $p |- ( ( A e. C /\ B e. D ) -> ( -. A ~<_ B <-> B ~< A ) ) $=
    ( wcel wa csdm wbr cdom wn domtri bicomd con1bid ) ACEBDEFZBAGHZABIHZNPOJAB
    CDKLM $.
    $( [16-Apr-2011] $)

  $( Omega strictly dominates a finite set.  (Contributed by FL,
     16-Apr-2011.) $)
  isfinite1b $p |- ( A e. Fin -> A ~< om ) $=
    ( cfn wcel com cdom wbr wn wa csdm isfinite1 omex ensym con3i anim2i brsdom
    cen sylibr syl ) ABCADEFZDAPFZGZHZADIFZAJUBSADPFZGZHUCUAUESUDTADKLMNADOQR
    $.
    $( [16-Apr-2011] $)

  $( The cross product of two finite sets is finite.  (Contributed by FL,
     16-Apr-2011.) $)
  cptwff $p |- ( ( A ~< om /\ B ~< om ) -> ( A X. B ) ~< om ) $=
    ( com csdm wbr wa cxp cfn wcel isfinite3 xpfi syl2anbr sylib ) ACDEZBCDEZFA
    BGZHIZPCDENAHIBHIQOAJBJABKLPJM $.
    $( [16-Apr-2011] $)

  $( The intersection of a non-empty element of a transitive class is a part of
     the class.  (Contributed by FL, 15-Apr-2011.) $)
  inttrp $p |- ( ( Tr A /\ B e. A /\ B =/= (/) ) -> |^| B C_ A ) $=
    ( wtr wcel c0 wne w3a cint wss wi wa trss imp cuni uniss df-tr sstr syl5com
    syl pm2.43i intssuni sstr2 3ad2ant3 ex com3l sylbi 3ad2ant1 3adant3 ) ACZBA
    DZBEFZGZBHZAIZUIUJULUNJZUKUIUJKBAIZUOUIUJUPABLMUPBNZANZIZULUNBAOULUSUNJZUIU
    JULUTJZUKUIURAIZVAAPUSVBULUNUSVBUOUSVBKUQAIZULUNUQURAQUKUIVCUNJZUJUKUMUQIVD
    BUAUMUQAUBSUCRUDUEUFUGTRSUHT $.
    $( [15-Apr-2011] $)


  $( The union of two transitive classes is transitive.  JFM CLASSES1. th. 55
     (Contributed by FL, 16-Apr-2011.) $)
  trunitr $p |- ( ( Tr A /\ Tr B ) -> Tr ( A u. B ) ) $=
    ( wtr wa cun cuni wss uniun df-tr unss12 syl2anb syl5eqss sylibr ) ACZBCZDZ
    ABEZFZQGQCPRAFZBFZEZQABHNSAGTBGUAQGOAIBISATBJKLQIM $.
    $( [16-Apr-2011] $)

  ${
    $d A x $.
    $( Union of a cumulative hierarchy of sets.  (Contributed by FL,
       23-Apr-2011.) $)
    uncum2 $p |- ( A e. On -> U_ x e. A ( R1 ` x ) C_ ( R1 ` A ) ) $=
      ( con0 wcel cv cr1 cfv wss wral ciun r1ord2 ralrimiv iunss sylibr ) BCDZA
      EZFGZBFGZHZABIABQJRHOSABPBKLABQRMN $.
      $( [23-Apr-2011] $)
  $}

  ${
    $d A x y $.
    $( If all the elements of a set ` A ` are ordinal numbers and are parts of
       the set then ` A ` is an ordinal number.  (Contributed by FL,
       20-Apr-2011.) $)
    celsor $p |- ( ( A e. B /\ A. x e. A ( x e. On /\ x C_ A ) )
      -> A e. On ) $=
      ( vy wcel cv con0 wss wral wtr wceq w3o r19.26 dftr3 biimpri eloni ralimi
      wa word sylbi eleq1 cbvralv raaanv ordtri3or syl2an sylbir expcom pm2.43i
      wi anim12i ancoms adantl wb elong adantr dford2 syl6bb mpbird ) BCEZAFZGE
      ZUTBHZRABIZRZBGEZBJZUTDFZEUTVGKVGUTELZDBIZABIZRZVCVKUSVCVAABIZVBABIZRVKVA
      VBABMVMVLVKVMVFVLVJVFVMABNOVLVJVLVGGEZDBIZVLVJUIVAVNADBUTVGGUAUBVLVOVJVLV
      ORVAVNRZDBIZABIVJVAVNADBUCVQVIABVPVHDBVAUTSVGSVHVNUTPVGPUTVGUDUEQQUFUGTUH
      UJUKTULVDVEBSZVKUSVEVRUMVCBCUNUOADBUPUQUR $.
      $( [20-Apr-2011] $)
  $}


  ${
    $( If a relation ` R ` is reflexive, it is included in ` ( R o. R ) ` .
       (Contributed by FL, 8-May-2011.) $)
    reflincror $p |- ( ( Rel R /\ ( _I |` U. U. R ) C_ R )
       -> R C_ ( R o. R ) ) $=
      ( wrel cid cuni cres wss ccom wceq relcoi2 coss1 sseq1 biimpd syl2im imp
      ) ABZCADDEZAFZAAAGZFZOPAGZAHZQTRFZSAIPAAJUAUBSTARKLMN $.
      $( [8-May-2011] $)
  $}

  ${
    fldrels.1 $e |- X = U. U. R $.
    $( The field of a relation is a set.  (Contributed by FL, 23-May-2011.) $)
    fldrels $p |- ( R e. S -> X e. _V ) $=
      ( wcel cuni cvv uniexg eqcomi eleq1i biimpi 3syl ) ABEAFZGEMFZGEZCGEZABHM
      GHOPNCGCNDIJKL $.
      $( [23-May-2011] $)
  $}

  ${
    $( Value when ` C ` doesn't belong to the domain.  (Contributed by FL,
       26-Jun-2011.) $)
    fvsnn $p |- ( C =/= A -> ( { <. A , B >. } ` C ) = (/) ) $=
      ( wne cop csn cdm wcel wn cfv c0 wceq dmsnop elsni necon3ai eleq2 syl5ibr
      wi notbid ax-mp ndmfv syl ) CADZCABEFZGZHZIZCUDJKLUEAFZLZUCUGRABMUCUGUICU
      HHZIUJCACANOUIUFUJUEUHCPSQTCUDUAUB $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $d D x $.
    fvsn2.1 $e |- A e. E $.
    fvsn2.2 $e |- B e. F $.
    fvsn2.3 $e |- C e. G $.
    fvsn2.4 $e |- D e. H $.
    $( Value of a function with a domain of two different values.  (Contributed
       by FL, 26-Jun-2011.) $)
    fvsn2a $p |- ( A =/= B ->
       ( { <. A , C >. , <. B , D >. } ` A ) = C ) $=
      ( elexi fvpr1 ) ABCDAEIMCGKMN $.
      $( [26-Jun-2011] $)

    $( Value of a function with a domain of two different values.  (Contributed
       by FL, 26-Jun-2011.) $)
    fvsn2b $p |- ( A =/= B ->
       ( { <. A , C >. , <. B , D >. } ` B ) = D ) $=
      ( elexi fvpr2 ) ABCDBFJMDHLMN $.
      $( [26-Jun-2011] $)
  $}

  $( A relation empty iff its converse is empty.  (Contributed by FL,
     19-Sep-2011.) $)
  cnveq3 $p |- ( Rel A -> ( A = (/) <-> `' A = (/) ) ) $=
    ( c0 ccnv wceq wrel wb wi cnv0 rel0 cnveqb mpan2 eqeq2 bibi2d syl5ibr ax-mp
    eqcoms ) BCZBDAEZABDZACZBDZFZGZHUCBQRUBBQDZSTQDZFZRBEUFIABJKUDUAUESBQTLMNPO
    $.
    $( [19-Sep-2011] $)

  ${
    $d R x $.
    $( A relation is reflexive iff its converse is reflexive.  (Contributed by
       FL, 19-Sep-2011.) $)
    relrefcnv $p |- ( Rel R -> ( ( _I |` U. U. R ) C_ R <->
      ( _I |` U. U. `' R ) C_ `' R ) ) $=
      ( vx wrel cv wbr cuni wral ccnv cid cres wss cnvref2 ref4w 3bitr3g ) ACBD
      ZOAEBAFFZGOOAHZEBQFFZGIPJAKIRJQKBALBPAMBRQMN $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F i x $.  $d G i x $.  $d I x $.
    $( If a family of sets ` A ` indexed by ` I ` covers the common domain
       ` B ` of two functions ` F ` and ` G ` , the restrictions of ` F ` and
       ` G ` to ` ( A i^i B ) ` are equal iff ` F = G ` .  Compare ~ eqfnun .
       (Contributed by FL, 5-Nov-2011.) $)
    eqfnung2 $p |- ( ( B C_ U_ i e. I A /\ F Fn B /\ G Fn B ) ->
    ( A. i e. I ( F |` A ) = ( G |` A ) <-> F = G ) ) $=
      ( vx wfn w3a cres wceq wral wa cv cfv wcel wi wrex fvres syl eqidd r19.29
      ciun wss ssel eliun fveq1 eqeq12 biimpd syl2anc mpan9 rexlimivw a1d a1i12
      com4r ex com4t 3imp com3l sylbi syl6com pm2.43a com14 pm2.43i 3expd 3imp1
      a1i ralrimiv jca wb 3simpc adantr eqfnfv mpbird reseq1 ralrimiva impbid1
      ) BCFAUCZUDZDBHZEBHZIZDAJZEAJZKZCFLZDEKZWBWFWGWBWFMZWGBBKZGNZDOZWJEOZKZGB
      LZMZWHWIWNWHBUAWHWMGBVSVTWAWFWJBPZWMQZVSVTWAWFWQVSVTWAWFIZWQQWPVSWRVSWMVS
      WPWRVSWMQZQZVSWPWJVRPZWPWTQZBVRWJUEXAWJAPZCFRZXBCWJFAUFWRXDWPWSVTWAWFXDWP
      WSQZQWFXDVTWAXEWFXDVTWAXEQQZWFXDMWEXCMZCFRZXFWEXCCFUBVTWAWPXHWSVTWAWPXHWS
      QZQXIWPXHWMVSXGWMCFWEWJWCOZWJWDOZKZXCWMWJWCWDUGXCXJWKKZXKWLKZXLWMQWJADSWJ
      AESXMXNMXLWMXJWKXKWLUHUIUJUKULUMVGUNUOTUPUQURUSUTVAVBVCVDVEVFVHVIWHVTWAMZ
      WGWOVJWBXOWFVSVTWAVKVLGBBDEVMTVNUPWGWECFWGWECNFPDEAVOVLVPVQ $.
      $( [5-Nov-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d R x y $.
    $( An application is injective if a retraction exists.  Bourbaki E.II.18
       prop. 8.  (Contributed by FL, 11-Nov-2011.) $)
    injrec $p |- ( ( F : A --> B /\ Fun R /\ ( R o. F ) = ( _I |` A ) )
         -> F : A -1-1-> B ) $=
      ( vx vy wfun wceq cv cfv wi wral wcel wa eleq2 eqcoms fvco eqtr ex syl wf
      ccom cid cres w3a weq wf1 simp1 cdm wb anbi12d biimpd ffun 3expia anim12d
      fdm fveq2 fveq1 fvresi com3l imp syl2an syl5com syl2im com23 mpcom expcom
      com4l com14 syl6 com24 com15 syli com25 3imp ralrimivv dff13 sylanbrc ) A
      BDUAZCGZCDUBZUCAUDZHZUEZVSEIZDJZFIZDJZHZEFUFZKZFALEALABDUGVSVTWCUHWDWKEFA
      AVSVTWCWEAMZWGAMZNZWKKZDUIZAHZVSVTWCWOKZKABDUPWQWNVTWCVSWKWNWQWEWPMZWGWPM
      ZNZVTWCVSWKKKKWQWNXAWNXAUJAWPAWPHWLWSWMWTAWPWEOAWPWGOUKPULVSWNVTWCXAWKVSD
      GZWNVTWCXAWKKKZKKABDUMVTXBWNXCVTXBWNXCKVTXBNZXAWCWNWKXDXAWEWAJZWFCJZHZWGW
      AJZWHCJZHZNZWRXDWSXGWTXJVTXBWSXGWECDQUNVTXBWTXJWGCDQUNUOWIWCWNXKWJWIXFXIH
      ZWCWNXKWJKKKWFWHCUQXKXLWCWNWJXGXJXLWCWNWJKZKZKXGXLXJXNXGXLXJXNKZXGXLNXEXI
      HXOXEXFXIRXOXIXEXJXIXEHZXNXJXPNXHXEHZWCXMXHXIXERXHWGWBJZHWCXQXMKZWGWAWBUR
      WCXSKXRXHXRXHHZXQWCXMXTXQXNXTXQNXRXEHZWCXEWEWBJZHZXMXRXHXERWEWAWBURYAYCXM
      YAYCNXRYBHZWNWJXRXEYBRWLYBWEHZXRWGHZYDWJKZWMAWEUSAWGUSYEYFYGYDYEYFWJYDYEY
      FWJKZYDYENXRWEHYHXRYBWERYHWEXRWEXRHYFWJWEXRWGRSPTSUTVAVBVCSVDSVEPVFVCVGPT
      SVEVAVHTVIVJVKSUTTVLVMVNVFVOVPEFABDVQVR $.
      $( [11-Nov-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d S x y $.
    $( An application is surjective if a section exists.  Bourbaki E.II.18
       prop. 8.  (Contributed by FL, 17-Nov-2011.) $)
    surjsec $p |- ( ( F : A --> B /\ S : B --> A /\ ( F o. S ) = ( _I |` B ) )
         -> F : A -onto-> B ) $=
      ( vy vx wf cid wceq w3a cfv wcel wi wa eqtr eqcoms syl ex com4l com3l wfo
      ccom cres cv wrex wral simp1 fveq1 fvres fvi jca wfun cdm adantl 3ad2ant1
      ffun adantr fdm eleq2 biimpd imp 3adant2 3jca fvco ffvelrn eqeq2d rcla4ev
      fveq2 com13 com23 com12 3imp 3syl com14 pm2.43i 3exp mpcom ralrimiv dffo3
      a1i sylanbrc ) ABDGZBACGZDCUBZHBUCZIZJZWBEUDZFUDZDKZIZFAUEZEBUFABDUAWBWCW
      FUGWGWLEBWBWCWFWHBLZWLMZWFWBWCWNWFWHWDKZWHWEKZIZWBWCWNMZMWHWDWEUHWMWQWBWC
      WLWPWHHKZIZWSWHIZNZWMWQWBWCWLMZMZMZWMWTXAWHBHUIWHBUJUKXBWPWHIZWMXEMWPWSWH
      OWQXFWMXDWQXFWMXDMZWQXFNWOWHIZXGWOWPWHOWCXHWMWBWLWCXHWMWBWLMZWCXHWMJZXIWB
      XJXJWLWBXJXJWLMMXJWBXJWBWLXJWBXJXIMZXJWBNZDULZCULZWHCUMZLZJWOWHCKZDKZIZXK
      XLXMXNXPWBXMXJABDUPUNXJXNWBWCXHXNWMBACUPUOUQXJXPWBWCWMXPXHWCWMXPWCXOBIWMX
      PMZBACURXTBXOBXOIWMXPBXOWHUSUTPQVAVBUQVCWHDCVDXJXSXIWCXHWMXSXIMZXHWCWMYAM
      XSXHWCWMXIXHWCWMXIMMZMXRWOXRWOIZXHYBYCXHNXRWHIZYBXRWOWHOWBYDWCWMWLYDWRMWB
      YDWMWCWLWMXCMWHXRWCWMWHXRIZWLWCWMYEWLMZWCWMNXQALZYFBAWHCVEYGYEWLWKYEFXQAW
      IXQIWJXRWHWIXQDVHVFVGRQRVIPVJVTSQRPSVKVLVKVMRVNVOTVOVPSQRTQVQSQTVLVRFEABD
      VSWA $.
      $( [17-Nov-2011] $)
  $}

  ${
    $d A r $.  $d B r $.  $d F r $.
    $( A function is an injection iff a retraction exists.  Bourbaki E.II.18
       prop. 8.  (Contributed by FL, 11-Nov-2011.) $)
    injrec2 $p |- ( ( F : A --> B /\ A e. C )
      -> ( F : A -1-1-> B <-> E. r ( Fun r /\
        ( r o. F ) = ( _I |` A ) ) ) ) $=
      ( wf wcel wa wf1 cv wfun ccom cid cres wceq cvv adantr adantl imp wi ccnv
      wex fex cnvexg df-f1 simprbi f1cocnv1 jca32 ex funeq coeq1 eqeq1d anbi12d
      syl cla4egv syl6 injrec 3exp com3l exlimiv com12 impbid ) ABDFZACGZHZABDI
      ZEJZKZVGDLZMANZOZHZEUBZVEVFDUAZPGZVNKZVNDLZVJOZHZHZVMVEVFVTVEVFHVOVPVRVEV
      OVFVEDPGVOABCDUCDPUDUNQVFVPVEVFVCVPABDUEUFRVFVRVEABDUGRUHUIVOVSVMVLVSEVNP
      VGVNOZVHVPVKVRVGVNUJWAVIVQVJVGVNDUKULUMUOSUPVCVMVFTVDVMVCVFVLVCVFTZEVHVKW
      BVCVHVKVFVCVHVKVFABVGDUQURUSSUTVAQVB $.
      $( [11-Nov-2011] $)
  $}

  ${
    $d A a b s $.  $d B b s $.  $d F a b s x y $.
    $( A function is an surjection iff a section exists.  Bourbaki E.II.18
       prop. 8.  (Contributed by FL, 18-Nov-2011.) $)
    surjsec2 $p |- ( ( F : A --> B /\ A e. C /\ B e. D )
      -> ( F : A -onto-> B <-> E. s ( s : B --> A /\
        ( F o. s ) = ( _I |` B ) ) ) ) $=
      ( va vb vx vy wf wcel cv wceq wa wex wb wi cfv wral ccom cres feq2 anbi1d
      wfo cid foeq2 feq3 exbidv bibi12d imbi12d foeq3 reseq2 anbi12d wrex dffo3
      eqeq2d eqcom biimpi reximi ralimi adantl sylbi vex fveq2 ac6 syl wfn fofn
      eqeq1d fnfco sylan fnresi eqfnfv2 sylancl wel w3a wfun cdm fofun 3ad2ant1
      3ad2ant2 fdm eqcomd eleq2d biimpa 3adant1 syl3anc fvresi 3ad2ant3 eqeq12d
      ffun fvco 3expa ralbidva bitrd pm5.32da mpbird surjsec 3exp com3l exlimiv
      imp com12 impbid2 vtocl2g ex com3r 3imp ) ABEKZACLZBDLZABEUEZBAFMZKZEXNUA
      ZUFBUBZNZOZFPZQZXKXLXJYAXKXLXJYARZGMZHMZEKZYCYDEUEZYDYCXNKZXPUFYDUBZNZOZF
      PZQZRAYDEKZAYDEUEZYDAXNKZYIOZFPZQZRYBGHABCDYCANZYEYMYLYRYCAYDEUCYSYFYNYKY
      QYCAYDEUGYSYJYPFYSYGYOYIYCAYDXNUHUDUIUJUKYDBNZYMXJYRYAYDBAEUHYTYNXMYQXTYD
      BAEULYTYPXSFYTYOXOYIXRYDBAXNUCYTYHXQXPYDBUFUMUQUNUIUJUKYEYFYKYFYKYGIMZXNS
      ZESZUUANZIYDTZOZFPZYFJMZESZUUANZJYCUOZIYDTZUUGYFYEUUAUUINZJYCUOZIYDTZOUUL
      JIYCYDEUPUUOUULYEUUNUUKIYDUUMUUJJYCUUMUUJUUAUUIURUSUTVAVBVCUUJUUDIJYDYCFH
      VDGVDUUHUUBNUUIUUCUUAUUHUUBEVEVJVFVGYFYJUUFFYFYGYIUUEYFYGOZYIUUAXPSZUUAYH
      SZNZIYDTZUUEUUPXPYDVHZYHYDVHYIUUTQYFEYCVHYGUVAYCYDEVIYCYDEXNVKVLYDVMIYDXP
      YHVNVOUUPUUSUUDIYDYFYGIHVPZUUSUUDQYFYGUVBVQZUUQUUCUURUUAUVCEVRZXNVRZUUAXN
      VSZLZUUQUUCNYFYGUVDUVBYCYDEVTWAYGYFUVEUVBYDYCXNWLWBYGUVBUVGYFYGUVBUVGYGYD
      UVFUUAYGUVFYDYDYCXNWCWDWEWFWGUUAEXNWMWHUVBYFUURUUANYGYDUUAWIWJWKWNWOWPWQU
      IWRYKYEYFYJYEYFRZFYGYIUVHYEYGYIYFYEYGYIYFYCYDXNEWSWTXAXCXBXDXEXFXGXHXI $.
      $( [18-Nov-2011] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d F x y $.
    $( Preimage of an intersection.  (Contributed by FL, 16-Apr-2012.) $)
    inpreima5 $p |- ( ( Fun F /\ A =/= (/) )
        -> ( `' F " |^|_ x e. A B ) = |^|_ x e. A ( `' F " B ) ) $=
      ( vy ciin cima wcel wral simpll cnvimass sseli adantl cvv fvimacnvi eliin
      wa wb ax-mp wi wfun wne ccnv cdm cfv fvex adantlr biimpa sylancr fvimacnv
      c0 cv ralbidv syl21anc vex sylibr biimpd ralimdv sylc wrex r19.2zb biimpi
      ex rexlimivw syl6 syl5bi imp syl2anc mpbid impbida eqrdv ) DUAZBUKUBZQZED
      UCZABCFZGZABVOCGZFZVNEULZVQHZVTVSHZVNWAQZVTVRHZABIZWBWCVLVTDUDZHZVTDUEZCH
      ZABIZWEVLVMWAJWAWGVNVQWFVTDVPKLMWCWHNHZWHVPHZWJVTDUFZVLWAWLVMVTVPDOUGWKWL
      WJAWHBCNPZUHUIVLWGQZWJWEWOWIWDABVTCDUJUMUHUNVTNHZWBWEREUOZAVTBVRNPZSZUPVN
      WBQZWLWAWTWJWLWTVLWEWJVLVMWBJZWBWEVNWPWBWETWQWPWBWEWRUQSMVLWDWIABVLWDWIVT
      CDOVCURUSWKWLWJRWMWNSUPWTVLWGWLWARXAVNWBWGVMWBWGTVLWBWEVMWGWSVMWEWDABUTZW
      GVMWEXBTWDABVAVBWDWGABVRWFVTDCKLVDVEVFMVGVTVPDUJVHVIVJVK $.
      $( [16-Apr-2012] $)
  $}

  ${
    $d A x $.  $d F x $.
    $( Preimage of an intersection.  (Contributed by FL, 28-Apr-2012.) $)
    inpreima2 $p |- ( ( Fun F /\ A =/= (/) )
        -> ( `' F " |^| A ) = |^|_ x e. A ( `' F " x ) ) $=
      ( wfun c0 wne wa ccnv cint cima cv ciin intiin imaeq2i inpreima5 syl5eq )
      CDBEFGCHZBIZJQABAKZLZJABQSJLRTQABMNABSCOP $.
      $( [28-Apr-2012] $)
  $}

  ${
    $d A x z $.  $d B y z $.  $d C z $.  $d D x z $.
    $( Existence of a class abstraction of existentially restricted sets.
       (Contributed by FL, 15-Oct-2012.) $)
    ab2rexexg2 $p |- ( ( A e. D /\ A. x e. A B e. E )
       -> { z | E. x e. A E. y e. B z = C } e. _V ) $=
      ( wcel wral cv wceq wrex cab cvv abrexexg ralimi abrexex2g sylan2 ) EHIZA
      DJDGICKFLBEMZCNOIZADJUAADMCNOITUBADBCEFHPQUAACDGORS $.
      $( [15-Oct-2012] $)
  $}


  ${
    $d A x z $.  $d B x z $.  $d B y z $.  $d C z $.
    $( Can I remove the distinct variables statement @d B x @. $)
    $( Existence of a class abstraction of existentially restricted sets.
       (Contributed by FL, 19-Apr-2012.) $)
    ab2rexexg $p |- ( ( A e. D /\ B e. E )
       -> { z | E. x e. A E. y e. B z = C } e. _V ) $=
      ( wcel cvv cv wceq wrex cab elex c0 cif ax-17 rexeq abbid eleq1d rexbidva
      wb adantr 0ex elimel ab2rexex dedth2h syl2an ) DGIDJIZEJIZCKFLZBEMZADMZCN
      ZJIZEHIDGOEHOUJUKUPUMAUJDPQZMZCNZJIULBUKEPQZMZAUQMZCNZJIDEPPDUQLZUOUSJVDU
      NURCVDCRUMADUQSTUAEUTLZUSVCJVEURVBCVECRVEUMVAAUQVEUMVAUCAKUQIULBEUTSUDUBT
      UAABCUQUTFDPJUEUFEPJUEUFUGUHUI $.
      $( [19-Apr-2012] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d D x y z $.
    ov2gc.1 $e |- O = { <. <. x , y >. , z >. |
      ( x e. C /\ y e. D /\ z = ( x o. y ) ) } $.
    $( Value of a composition. ~ ov2g adapted to this special case of a
       composite.  (Contributed by FL, 14-Jul-2012.) $)
    ov2gc $p |- ( ( A e. C /\ B e. D ) -> ( A O B ) = ( A o. B ) ) $=
      ( wcel ccom cvv co wceq coexg cv coeq1 coeq2 copab2 wa w3a oprabbii eqtri
      df-3an ov2g mpd3an3 ) DFJEGJDEKZLJDEHMUGNDEFGOABCDEFGAPZBPZKZUGHDUIKLUHDU
      IQUIEDRHUHFJZUIGJZCPUJNZUAZABCSUKULTUMTZABCSIUNUOABCUKULUMUDUBUCUEUF $.
      $( [14-Jul-2012] $)
  $}

  ${
    $d C x y z $.  $d D x y z $.
    ov4gc.1 $e |- O = { <. <. x , y >. , z >. |
      ( x e. C /\ y e. D /\ z = ( x o. y ) ) } $.
    $( Value of a composition. ~ ov4g adapted to the special case of a
       composite.  (Contributed by FL, 14-Jul-2012.) $)
    ov4gc $p |- ( ( x e. C /\ y e. D ) -> ( x O y ) = ( x o. y ) ) $=
      ( cv wcel ccom cvv co wceq coexg w3a copab2 wa df-3an oprabbii eqtri ov4g
      mpd3an3 ) AHZDIZBHZEIZUCUEJZKIUCUEFLUGMUCUEDENABCDEUGKFFUDUFCHUGMZOZABCPU
      DUFQUHQZABCPGUIUJABCUDUFUHRSTUAUB $.
      $( [14-Jul-2012] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C z $.
    elovdm.1 $e |-
      F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ z = C ) } $.
    elovdm.2 $e |- C e. H $.
    $( If an operation value has a member, the first argument belongs to the
       first projection of the domain.  (Contributed by FL, 15-Oct-2012.) $)
    elovdm $p |- ( D e. ( E F G ) -> E e. A ) $=
      ( wcel cop cdm cv wa wceq ax-mp cfv co elfvdm cxp opelxp1 wrel dmoprabss6
      copab2 relxp wb twsvbdop eqtr dmeq eqeq1d syl expcom eqcoms mpbiri eleq2s
      wi df-ov ) HDNZGHJOZIUAZHJIUBGVDNVCIPZNVBGVCIUCVBVCDEUDZVEHJDEUEIAQZDNBQZ
      ENRCQFSZRABCUHZSZVEVFSZLVKVLVGVHOVFNVIRABCUHZPZVFSZVFUFVODEUIABCVFFKMUGTV
      MVJSVKVLVOUJZUTZVIABCDEUKVQVJVMVKVJVMSZVPVKVRRIVMSZVPIVJVMULVSVEVNVFIVMUM
      UNUOUPUQTURTUSUOHJIVAUS $.
      $( [15-Oct-2012] $)

    $( If an operation value has a member, the second argument belongs to the
       second projection of the domain.  (Contributed by FL, 15-Oct-2012.) $)
    elovdm2 $p |- ( G e. I ->
       ( D e. ( E F G ) -> ( E e. A /\ G e. B ) ) ) $=
      ( wcel wa wi cdm cv eleq2s cop cxp cfv elfvdm wceq copab2 dmoprabss sseli
      co dmeqi a1i syl5com df-ov com12 opelxpg sylibd ) JLOZGHJIUIZOZHJUAZDEUBZ
      OZHDOJEOPUSUQVBUQVBQGUTIUCZURGVCOUTIRZOZUQVBGUTIUDVEVBQUQVBUTASDOBSEOPCSF
      UEZPABCUFZRZVDVHVAUTVFABCDEUGUHIVGMUJTUKULHJIUMTUNHJDELUOUP $.
      $( [15-Oct-2012] $)

  $}

  ${
    $d A y $.  $d R x y $.  $d i y $.
    $( The domain of the intersection of a family of reflexive classes is the
       intersection of the domains.  (Contributed by FL, 15-Oct-2012.) $)
    domintrefc $p |- ( A. i e. A A. x e. dom R x R x ->
      dom |^|_ i e. A R = |^|_ i e. A dom R ) $=
      ( vy cv wbr cdm wral ciin wss dmiin a1i wcel cvv wb eliin ax-mp wa sylbi
      wi vex r19.26 weq id breq12d rcla4va ralimi cop ralbii opex bicomd opeldm
      df-br syl sylbir ex com12 ralrimiv dfss3 sylibr eqssd ) AFZVCCGZACHZIZDBI
      ZDBCJZHZDBVEJZVIVJKVGDBCLMVGEFZVINZEVJIVJVIKVGVLEVJVKVJNZVGVLVMVKVENZDBIZ
      VGVLUAVKONVMVOPEUBZDVKBVEOQRVOVGVLVOVGSVNVFSZDBIZVLVNVFDBUCVRVKVKCGZDBIZV
      LVQVSDBVDVSAVKVEAEUDZVCVKVCVKCWAUEZWBUFUGUHVTVKVKUIZCNZDBIZVLVSWDDBVKVKCU
      NUJWEWCVHNZVLWCONZWEWFPVKVKUKWGWFWEDWCBCOQULRVKVKVHVPUMTTUOUPUQTURUSEVJVI
      UTVAVB $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d A x $.
    $( The range of an intersection is a part of the intersection of the
       ranges.  (The case ` A = (/) ` works as well, the intersection gives
       ` _V ` ).  (Contributed by FL, 15-Oct-2012.) $)
    rnintintrn $p |- ran |^|_ x e. A B C_ |^|_ x e. A ran B $=
      ( ciin crn wss c0 wceq cvv iineq1 0iin eqtr ssv sseq2 mpbiri ccnv cdm a1i
      wa df-rn syl sylancl cnviin dmeqd dmiin eqsstrd cv wcel iineq2i pm2.61ine
      wne 3sstr4g ) ABCDZEZABCEZDZFZBGBGHUPAGUODZHZURIHZUQABGUOJAUOKUSUTSUPIHZU
      QUPURILVAUQUNIFUNMUPIUNNOUAUBBGUKZUMPZQZABCPZQZDZUNUPVBVDABVEDZQZVGVBVCVH
      ABCUCUDVIVGFVBABVEUERUFUMTABUOVFUOVFHAUGBUHCTRUIULUJ $.
      $( [15-Oct-2012] $)
  $}

  $( Projection of a part of a cross product.  (Contributed by FL,
     15-Oct-2012.) $)
  prjpacp1 $p |- ( ( B =/= (/) /\ C C_ ( A X. B ) ) -> ( 1st " C ) C_ A ) $=
    ( cxp wss c0 wne c1st cima imass2 wi prjcp1 sseq2 biimpd syl syl5com impcom
    wceq ) CABDZEZBFGZHCIZAEZTUBHSIZEZUAUCCSHJUAUDARZUEUCKABLUFUEUCUDAUBMNOPQ
    $.
    $( [15-Oct-2012] $)

  $( Projection of a part of a cross product.  (Contributed by FL,
     15-Oct-2012.) $)
  prjpacp2 $p |- ( ( A =/= (/) /\ C C_ ( A X. B ) ) -> ( 2nd " C ) C_ B ) $=
    ( cxp wss c0 wne c2nd cima imass2 wi prjcp2 sseq2 biimpd syl syl5com impcom
    wceq ) CABDZEZAFGZHCIZBEZTUBHSIZEZUAUCCSHJUAUDBRZUEUCKABLUFUEUCUDBUBMNOPQ
    $.
    $( [15-Oct-2012] $)

  $( Composition of the first member function with another function.
     (Contributed by FL, 15-Oct-2012.) $)
  2ndcof $p |- ( F : A --> ( B X. C ) -> ( 2nd o. F ) : A --> C ) $=
    ( cxp wf c2nd ccom wfn crn wss cvv wfo fo2nd fofn ax-mp ffn dffn2 cres frn
    sylib fnfco sylancr rnco rnss 3syl f2ndres a1i sstrd syl5eqss df-f sylanbrc
    ssres2 ) ABCEZDFZGDHZAIZUPJZCKACUPFUOGLIZALDFZUQLLGMUSNLLGOPUODAIUTAUNDQADR
    UALAGDUBUCUOURGDJZSZJZCGDUDUOVCGUNSZJZCUOVAUNKVBVDKVCVEKAUNDTVAUNGUMVBVDUEU
    FVECKZUOUNCVDFVFBCUGUNCVDTPUHUIUJACUPUKUL $.
    $( [15-Oct-2012] $)

  $( A relation is included in the cross product of its projections.
     (Contributed by FL, 15-Oct-2012.) $)
  relinccppr $p |- ( Rel A -> A C_ ( ( 1st " A ) X. ( 2nd " A ) ) ) $=
    ( wrel c1st cima c2nd cxp wss cdm wceq crn wi prjdmn prjrn xpeq12 relssdmrn
    wa sseq2 syl5ibr syl syl2anc pm2.43i ) ABZACADZEADZFZGZUBUCAHZIZUDAJZIZUBUF
    KZALAMUHUJPUEUGUIFZIZUKUCUGUDUINUBUFUMAULGAOUEULAQRSTUA $.
    $( [15-Oct-2012] $)

  ${
    $d x y z w A $.  $d x y z a $.  $d y z w F $.  $d F a $.
    dffn5a.1 $e |- ( a e. F -> A. x a e. F ) $.
    $( Representation of a function in terms of its values.  (Contributed by
       FL, 14-Sep-2013.) $)
    dffn5a $p |- ( F Fn A <->
                   F = { <. x , y >. | ( x e. A /\ y = ( F ` x ) ) } ) $=
      ( vz vw wfn cv wcel cfv wceq wa wb wal vex ax-17 anbi12d wrel cop fnop ex
      copab pm4.71rd eqcom fnopfvb syl5bb pm5.32da bitr4d hbfv hbeq eleq1 fveq2
      eqeq2d biidd eqeq1 opelopabf syl6bbr alrimivv fnrel relopab eqrel sylancl
      hban mpbird fvex eqid fnopab2 fneq1 mpbiri impbii ) DCIZDAJZCKZBJZVNDLZMZ
      NZABUDZMZVMWAGJZHJZUAZDKZWDVTKZOZHPGPZVMWGGHVMWEWBCKZWCWBDLZMZNZWFVMWEWIW
      ENWLVMWEWIVMWEWICWBWCDUBUCUEVMWIWKWEWKWJWCMVMWINWEWCWJUFCWBWCDHQZUGUHUIUJ
      VSWIVPWJMZNWLABWBWCWIWNAWIARAEEVPWJEJZVPKARAEWBDFWOWBKARUKULVEWLBRGQWMVNW
      BMZVOWIVRWNVNWBCUMWPVQWJVPVNWBDUNUOSVPWCMZWIWIWNWKWQWIUPVPWCWJUQSURUSUTVM
      DTVTTWAWHOCDVAVSABVBGHDVTVCVDVFWAVMVTCIABCVQVTVNDVGVTVHVICDVTVJVKVL $.
      $( [14-Sep-2013] $)
  $}

  $( A function's value belongs to its codomain.  (Contributed by FL,
     14-Sep-2013.) $)
  ffvelrnb $p |- ( ( A e. D /\ B e. E ) -> ( ( F e. ( B ^m A ) /\ C e. A )
    -> ( F ` C ) e. B ) ) $=
    ( wcel wa cmap co cfv wf wi wb elmapg ancoms ffvelrn ex syl6bi imp3a ) ADGZ
    BEGZHZFBAIJGZCAGZCFKBGZUCUDABFLZUEUFMUBUAUDUGNBAFEDOPUGUEUFABCFQRST $.
    $( [14-Sep-2013] $)

  $( opabssxp dfoprab2 $)

  ${
    $d x z A $.  $d x z B $.  $d y z B $.
    $( Existence of an existentially restricted class abstraction. ` ph ` is
       normally has free-variable parameters ` x ` , ` y ` , and ` z ` .
       Compare ~ abrexex2g .  (Contributed by FL, 6-Nov-2013.) $)
    ab2rexex2g $p |- ( ( A e. _V /\ B e. _V /\
         A. x e. A A. y e. B { z | ph } e. _V )
        -> { z | E. x e. A E. y e. B ph } e. _V ) $=
      ( cvv wcel cab wral w3a wrex wa simp1 wi simplr simpr jca ex abrexex2g
      syl6 ralimdv 3imp syl ) EGHZFGHZADIGHCFJZBEJZKZUEACFLZDIGHZBEJZMUJBELDIGH
      UIUEULUEUFUHNUEUFUHULUEUFUHULOUEUFMZUGUKBEUMUGUFUGMZUKUMUGUNUMUGMUFUGUEUF
      UGPUMUGQRSACDFGGTUAUBSUCRUJBDEGGTUD $.
      $( [6-Nov-2013] $)
  $}

  ${
    $d ph w $.  $d ps w $.  $d x w $.  $d y w $.  $d z w $.
    $( Equivalence of ordered pair abstraction subclass and implication.
       Compare ~ ssopab2 .  (Contributed by FL, 6-Nov-2013.) $)
    ssopab3 $p |- ( A. x A. y A. z ( ph -> ps ) ->
        { <. <. x , y >. , z >. | ph } C_
        { <. <. x , y >. , z >. | ps } ) $=
      ( vw wi wal cv cop wceq wa wex cab copab2 hba1 hba2 a4s eximd df-oprab
      ax4 anim2d ss2abdv 3sstr4g ) ABGZEHZDHZCHZFICIDIJEIJKZALZEMZDMZCMZFNUIBLZ
      EMZDMZCMZFNACDEOBCDEOUHUMUQFUHULUPCUGCPUGULUPGCUGUKUODUFDPUGUJUNEUEEDQUGA
      BUIUFUEDUEEUARUBSSRSUCACDEFTBCDEFTUD $.
      $( [6-Nov-2013] $)
  $}


  ${
    $d ph w $.  $d ph v $.  $d ps w $.  $d ps v $.  $d x w $.  $d x v $.
    $d y w $.  $d y v $.  $d z w $.  $d z v $.  $d x y z $.
    $( Equivalence of ordered pair abstraction subclass and implication.
       Compare ~ ssopab2b .  (Contributed by FL, 6-Nov-2013.) $)
    ssopab3b $p |- ( { <. <. x , y >. , z >. | ph } C_
      { <. <. x , y >. , z >. | ps } <->
               A. x A. y A. z ( ph -> ps ) ) $=
      ( vv vw copab2 wss wi wal hboprab1 hboprab2 hboprab3 cv cop wex wa alrimi
      hbss wceq opex isseti cab df-oprab sseq12i ss2ab copsexgb imbi12d syl5ibr
      ax-4 sylbi syl5bi exlimiv ax-mp ssopab3 impbii ) ACDEHZBCDEHZIZABJZEKZDKZ
      CKUTVCCCFURUSACDEFLBCDEFLTUTVBDDFURUSACDEFMBCDEFMTUTVAEEFURUSACDEFNBCDEFN
      TGOZCODOPZEOZPZUAZGQUTVAJZGVGVEVFUBUCVHVIGUTVHAREQDQCQZGUDZVHBREQDQCQZGUD
      ZIZVHVAURVKUSVMACDEGUEBCDEGUEUFVNVAVHVJVLJZVNVOGKVOVJVLGUGVOGUKULVHAVJBVL
      ACDEVDUHBCDEVDUHUIUJUMUNUOSSSABCDEUPUQ $.
      $( [6-Nov-2013] $)
  $}

  ${
    $( A function with a domain of two elements.  (Contributed by FL,
       2-Feb-2014.) $)
    fprg $p |- ( ( ( A e. E /\ B e. F ) /\ ( C e. G /\ D e. H ) /\ A =/= B )
                -> { <. A , C >. , <. B , D >. } : { A , B } --> { C , D } ) $=
      ( wcel wa cpr cop wf cvv wi elex c0 cif wceq 0ex wne anim12i neeq1 preq1d
      opeq1 preq1 feq12d imbi12d neeq2 preq2d preq2 opeq2 feq123 syl3anc imbi2d
      wb eqidd elimel fpr dedth4h syl2an 3impia ) AEIZBFIZJZCGIZDHIZJZABUAZABKZ
      CDKZACLZBDLZKZMZVEANIZBNIZJCNIZDNIZJVIVOOZVHVCVPVDVQAEPBFPUBVFVRVGVSCGPDH
      PUBVPVQVRVSVTVPAQRZBUAZWABKZVKWACLZVMKZMZOWAVQBQRZUAZWAWGKZVKWDWGDLZKZMZO
      WHWIVRCQRZDKZWAWMLZWJKZMZOWHWIWMVSDQRZKZWOWGWRLZKZMZOABCDQQQQAWASZVIWBVOW
      FAWABUCXCVJWCVKVNWEXCVLWDVMAWACUEUDAWABUFUGUHBWGSZWBWHWFWLBWGWAUIXDWCWIVK
      WEWKXDVMWJWDBWGDUEUJBWGWAUKUGUHCWMSZWLWQWHXEWKWPSWIWISZVKWNSWLWQUPXEWDWOW
      JCWMWAULUDXEWIUQCWMDUFWIVKWIWNWKWPUMUNUODWRSZWQXBWHXGWPXASXFWNWSSWQXBUPXG
      WJWTWODWRWGULUJXGWIUQDWRWMUKWIWNWIWSWPXAUMUNUOWAWGWMWRAQNTURBQNTURCQNTURD
      QNTURUSUTVAVB $.
      $( [2-Feb-2014] $)
  $}


  ${
    $( Members of an unordered triple of classes.  (Contributed by FL,
       2-Feb-2014.) $)
    eltpg $p |- ( A e. V -> ( A e. { B , C , D } <->
       ( A = B \/ A = C \/ A = D ) ) ) $=
      ( wcel cvv ctp wceq w3o wb elex c0 cif eleq1 3orbi123d bibi12d 0ex elimel
      eqeq1 eltp dedth syl ) AEFAGFZABCDHZFZABIZACIZADIZJZKZAELUDUKUDAMNZUEFZUL
      BIZULCIZULDIZJZKAMAULIZUFUMUJUQAULUEOURUGUNUHUOUIUPAULBTAULCTAULDTPQULBCD
      AMGRSUAUBUC $.
      $( [2-Feb-2014] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.
    $( Condition for a closed interval to be a subset of another closed
       interval.  See iccss (Contributed by FL, 29-May-2014.) $)
    iccss3 $p |- ( ( ( A e. RR* /\ B e. RR* ) /\ ( C e. RR* /\ D e. RR* ) /\
      ( A <_ C /\ D <_ B ) ) ->
        ( C [,] D ) C_ ( A [,] B ) ) $=
      ( vx cxr wcel wa cle wbr w3a cicc co cv simpr1 xrletr imp syl32anc elicc1
      wb simpl1l simpl2l simpl3l simpr2 simpl2r simpl1r simpr3 simpl3r 3ad2ant2
      3jca ex 3ad2ant1 3imtr4d ssrdv ) AFGZBFGZHZCFGZDFGZHZACIJZDBIJZHZKZECDLMZ
      ABLMZVDENZFGZCVGIJZVGDIJZKZVHAVGIJZVGBIJZKZVGVEGZVGVFGZVDVKVNVDVKHZVHVLVM
      VDVHVIVJOZVQUOURVHVAVIVLUOUPUTVCVKUAURUSUQVCVKUBVRVAVBUQUTVKUCVDVHVIVJUDU
      OURVHKVAVIHVLACVGPQRVQVHUSUPVJVBVMVRURUSUQVCVKUEUOUPUTVCVKUFVDVHVIVJUGVAV
      BUQUTVKUHVHUSUPKVJVBHVMVGDBPQRUJUKUTUQVOVKTVCCDVGSUIUQUTVPVNTVCABVGSULUMU
      N $.
      $( [29-May-2014] $)
  $}

  $( An element of a closed interval is more than or equal to its lower bound.
     (Contributed by FL, 29-May-2014.) $)
  iccleub2 $p |- ( ( A e. RR* /\ B e. RR* /\ C e. ( A [,] B ) ) -> A <_ C ) $=
    ( cxr wcel cicc co cle wbr wa w3a elicc1 simp2 syl6bi 3impia ) ADEZBDEZCABF
    GEZACHIZPQJRCDEZSCBHIZKSABCLTSUAMNO $.
    $( [29-May-2014] $)

  $( An element of a closed interval is less than or equal to its upper bound.
     (Contributed by FL, 29-May-2014.) $)
  iccleub3 $p |- ( ( A e. RR* /\ B e. RR* /\ C e. ( A [,] B ) ) -> C e. RR* )
      $=
    ( cxr wcel cicc co wa cle wbr w3a elicc1 simp1 syl6bi 3impia ) ADEZBDEZCABF
    GEZCDEZPQHRSACIJZCBIJZKSABCLSTUAMNO $.
    $( [29-May-2014] $)


  $( A way of proving that an extended real is real.  (Contributed by FL,
     29-May-2014.) $)
  xrre3 $p |- ( ( ( A e. RR* /\ B e. RR ) /\ ( B <_ A /\ A < +oo ) ) ->
              A e. RR ) $=
    ( cxr wcel cr wa cle wbr cpnf clt wb xrrebnd ad2antrr mnflt adantl wi mnfxr
    cmnf a1i rexr simpl xrltletr syl3anc mpand imp adantrr simprr mpbir2and ) A
    CDZBEDZFZBAGHZAIJHZFZFAEDZRAJHZUMUIUOUPUMFKUJUNALMUKULUPUMUKULUPUKRBJHZULUP
    UJUQUIBNOUKRCDZBCDZUIUQULFUPPURUKQSUJUSUIBTOUIUJUARBAUBUCUDUEUFUKULUMUGUH
    $.
    $( [29-May-2014] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    $( Membership in a closed-below, open-above real interval.  (Contributed by
       FL, 29-May-2014.) $)
    elico3 $p |- ( ( A e. RR /\ B e. RR* ) ->
        ( C e. ( A [,) B ) <-> ( C e. RR /\ A <_ C /\ C < B ) ) ) $=
      ( cr wcel cxr wa cico wbr clt w3a rexr cpnf wi wceq imp com13 breq2 com12
      cmnf co cle wb elico1 sylan simpr1 simpll simpr2 w3o elxr simpl1 3ad2ant3
      ltpnf adantr pnfxr a1i simpl2 simpr xrlttr syl32anc 3exp1 mpcom biimpd wn
      adantld nltmnf pm2.24 syl6bi syl 3jaoi sylbi adantl impcom xrre3 syl22anc
      3adant2 simpr3 3jca ex 3anim1i impbid1 bitrd ) ADEZBFEZGZCABHUAEZCFEZACUB
      IZCBJIZKZCDEZWHWIKZWCAFEWDWFWJUCALABCUDUEWEWJWLWEWJWLWEWJGZWKWHWIWMWGWCWH
      CMJIZWKWEWGWHWIUFWCWDWJUGWEWGWHWIUHZWJWEWNWGWIWEWNNWHWEWGWIGZWNWDWPWNNZWC
      WDBDEZBMOZBTOZUIWQBUJWRWQWSWTBMJIZWRWQBUMWPWRXAWNWGWIWRXAWNNNWGWIWRXAWNWG
      WIWRKZXAGZWGWDMFEZWIXAWNWGWIWRXAUKXBWDXAWRWGWDWIBLULUNXDXCUOUPWGWIWRXAUQX
      BXAURWGWDXDKWIXAGWNCBMUSPUTVAPQVBWSWIWNWGWSWIWNBMCJRVCVEWPWTWNWGWIWTWNNZW
      GCTJIZVDZWIXENCVFWTWIXGWNWTWIXFXGWNNBTCJRXFWNVGVHQVIPSVJVKVLSVPVMCAVNVOWO
      WEWGWHWIVQVRVSWKWGWHWICLVTWAWB $.
      $( [29-May-2014] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    $( Membership in a closed-below, open-above real interval.  (Contributed by
       FL, 29-May-2014.) $)
    elioc3 $p |- ( ( A e. RR* /\ B e. RR ) ->
        ( C e. ( A (,] B ) <-> ( C e. RR /\ A < C /\ C <_ B ) ) ) $=
      ( cxr wcel cr wa cioc clt wbr rexr cmnf wi cpnf wceq 3ad2ant2 breq1 com12
      w3a imp co cle wb elioc1 sylan2 simpr1 simplr w3o elxr mnflt mnfxr simp31
      a1i simp1 simp32 xrlttr syl32anc 3exp mpcom wn pnfnlt pm2.24 syl6bi com13
      syl 3adant3 biimpcd 3jaoi adantr simpr3 xrre syl22anc simpr2 3jca 3anim1i
      sylbi ex impbid1 bitrd ) ADEZBFEZGZCABHUAEZCDEZACIJZCBUBJZSZCFEZWEWFSZWAV
      TBDEWCWGUCBKABCUDUEWBWGWIWBWGWIWBWGGZWHWEWFWJWDWALCIJZWFWHWBWDWEWFUFVTWAW
      GUGWBWGWKVTWGWKMZWAVTAFEZANOZALOZUHWLAUIWMWLWNWOLAIJZWMWLAUJWPWMWGWKWPWMW
      GSZLDEZVTWDWPWEWKWRWQUKUMWMWPVTWGAKPWPWMWDWEWFULWPWMWGUNWPWMWDWEWFUOWRVTW
      DSWPWEGWKLACUPTUQURUSWGWNWKWDWEWNWKMZWFWDWEWSWDNCIJZUTZWEWSMCVAWNWEXAWKWN
      WEWTXAWKMANCIQWTWKVBVCVDVETVFRWGWOWKWEWDWOWKMWFWOWEWKALCIQVGPRVHVPVITWBWD
      WEWFVJZCBVKVLWBWDWEWFVMXBVNVQWHWDWEWFCKVOVRVS $.
      $( [29-May-2014] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A member of a closed interval is an extended real.  (Contributed by FL,
       29-May-2014.) $)
    eliccreb $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A [,] B ) C_ RR* ) $=
      ( vx cxr wcel wa cicc co cv cle wbr w3a elicc1 simp1 syl6bi ssrdv ) ADEBD
      EFZCABGHZDQCIZRESDEZASJKZSBJKZLTABSMTUAUBNOP $.
      $( [29-May-2014] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A member of a closed interval is an extended real.  (Contributed by FL,
       29-May-2014.) $)
    eliccre2b $p |- ( ( A e. RR /\ B e. RR ) -> ( A [,] B ) C_ RR ) $=
      ( iccssre ) ABC $.
      $( [29-May-2014] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A member of a closed-below, open-above interval is an extended real.
       (Contributed by FL, 29-May-2014.) $)
    eliocreb $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,] B ) C_ RR* ) $=
      ( vx cxr wcel wa cioc co cv clt wbr cle w3a elioc1 simp1 syl6bi ssrdv ) A
      DEBDEFZCABGHZDRCIZSETDEZATJKZTBLKZMUAABTNUAUBUCOPQ $.
      $( [29-May-2014] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A member of a closed-below, open-above interval is an extended real.
       (Contributed by FL, 29-May-2014.) $)
    eliocreb2 $p |- ( ( A e. RR* /\ B e. RR ) -> ( A (,] B ) C_ RR ) $=
      ( vx cxr wcel cr wa cioc co cv clt wbr cle w3a elioc3 simp1 syl6bi ssrdv
      ) ADEBFEGZCABHIZFSCJZTEUAFEZAUAKLZUABMLZNUBABUAOUBUCUDPQR $.
      $( [29-May-2014] $)
  $}

  ${
    $d x B $.  $d x C $.
    $( A member of a closed interval is an extended real.  (Contributed by FL,
       29-May-2014.) $)
    elicore $p |- ( ( B e. RR* /\ C e. RR* ) ->
     ( B [,) C ) C_ RR* ) $=
      ( vx cxr wcel wa cico co cv cle wbr clt w3a elico1 simp1 syl6bi ssrdv ) A
      DEBDEFZCABGHZDRCIZSETDEZATJKZTBLKZMUAABTNUAUBUCOPQ $.
      $( [29-May-2014] $)
  $}

  ${
    $d x B $.  $d x C $.
    $( A member of a closed interval is an extended real.  (Contributed by FL,
       29-May-2014.) $)
    elicore2 $p |- ( ( B e. RR /\ C e. RR* ) ->
    ( B [,) C ) C_ RR ) $=
      ( vx cr wcel cxr wa cico co cv cle wbr clt w3a elico3 simp1 syl6bi ssrdv
      ) ADEBFEGZCABHIZDSCJZTEUADEZAUAKLZUABMLZNUBABUAOUBUCUDPQR $.
      $( [29-May-2014] $)
  $}

  $( Absorption law for intersection.  (Contributed by FL, 30-May-2014.) $)
  inabs2 $p |- ( B i^i ( A u. B ) ) = B $=
    ( cun cin uncom ineq2i inabs eqtri ) BABCZDBBACZDBIJBABEFBAGH $.
    $( [30-May-2014] $)

  ${
    $d a b c x A $.  $d a b c z A $.  $d a b c x D $.  $d a b c x E $.
    $d a b c x F $.  $d b c x B $.  $d c x C $.  $d z B $.  $d z C $.
    $( Two ways of saying that two triples have no common element.
       (Contributed by FL, 29-May-2014.) $)
    inttpemp $p |- ( ( A e. G /\ B e. H /\ C e. I ) ->
    ( ( { A , B , C } i^i { D , E , F } ) = (/) <->
       ( ( A =/= D /\ A =/= E /\ A =/= F )
    /\ ( B =/= D /\ B =/= E /\ B =/= F )
    /\ ( C =/= D /\ C =/= E /\ C =/= F ) ) ) ) $=
      ( vx vb vc wceq wcel wn w3a wne ax-17 neeq1 df-ne va vz ctp cin wral disj
      c0 cv wb tpeq1 eleq2d imbi1d ralbidv2 3anbi1d bibi12d tpeq2 3anbi2d tpeq3
      3anbi123d 3anbi3d wsb vex raltp sbn cvv sbcel1gv ax-mp notbii eltp 3ioran
      w3o bicomi 3anbi123i 3bitri bitri vtocl3gf syl5bb ) ABCUCZDEFUCZUDUGMJUHZ
      VSNZOZJVRUEZAGNBHNCINPADQZAEQZAFQZPZBDQZBEQZBFQZPZCDQZCEQZCFQZPZPZJVRVSUF
      WBJUAUHZKUHZLUHZUCZUEZWQDQZWQEQZWQFQZPZWRDQZWREQZWRFQZPZWSDQZWSEQZWSFQZPZ
      PZUIWBJAWRWSUCZUEZWGXIXMPZUIZWBJABWSUCZUEZWGWKXMPZUIZWCWPUIZUAKLUBABCGHIU
      BUHZANZUARYEKRYELRYDBNZKRYFLRYDCNLRXRUARYBKRYCLRWQAMZXAXPXNXQYGWBWBJWTXOY
      GVTWTNVTXONZWBYGWTXOVTWQAWRWSUJUKULUMYGXEWGXIXMYGXBWDXCWEXDWFWQADSWQAESWQ
      AFSUSUNUOWRBMZXPXTXQYAYIWBWBJXOXSYIYHVTXSNZWBYIXOXSVTWRBAWSUPUKULUMYIXIWK
      WGXMYIXFWHXGWIXHWJWRBDSWRBESWRBFSUSUQUOWSCMZXTWCYAWPYKWBWBJXSVRYKYJVTVRNW
      BYKXSVRVTWSCABURUKULUMYKXMWOWGWKYKXJWLXKWMXLWNWSCDSWSCESWSCFSUSUTUOXAWBJU
      AVAZWBJKVAZWBJLVAZPXNWBJWQWRWSUAVBZKVBZLVBZVCYLXEYMXIYNXMYLWAJUAVAZOWQVSN
      ZOZXEWAJUAVDYRYSWQVENYRYSUIYOJWQVSVEVFVGVHYTWQDMZWQEMZWQFMZVKZOUUAOZUUBOZ
      UUCOZPXEYSUUDWQDEFYOVIVHUUAUUBUUCVJUUEXBUUFXCUUGXDXBUUEWQDTVLXCUUFWQETVLX
      DUUGWQFTVLVMVNVNYMWAJKVAZOWRVSNZOZXIWAJKVDUUHUUIWRVENUUHUUIUIYPJWRVSVEVFV
      GVHUUJWRDMZWREMZWRFMZVKZOUUKOZUULOZUUMOZPXIUUIUUNWRDEFYPVIVHUUKUULUUMVJUU
      OXFUUPXGUUQXHXFUUOWRDTVLXGUUPWRETVLXHUUQWRFTVLVMVNVNYNWAJLVAZOWSVSNZOZXMW
      AJLVDUURUUSWSVENUURUUSUIYQJWSVSVEVFVGVHUUTWSDMZWSEMZWSFMZVKZOUVAOZUVBOZUV
      COZPXMUUSUVDWSDEFYQVIVHUVAUVBUVCVJUVEXJUVFXKUVGXLXJUVEWSDTVLXKUVFWSETVLXL
      UVGWSFTVLVMVNVNVMVOVPVQ $.
      $( [29-May-2014] $)
  $}


  $( End of General Set Theory. $)

