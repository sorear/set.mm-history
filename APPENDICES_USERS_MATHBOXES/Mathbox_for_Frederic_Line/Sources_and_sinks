$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Sources and sinks
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Source $.

  $( Extend class notation with the class of all sources. $)
  csrce $a class Source $.

  ${
    $d c i s x y $.
    $( A source is a family ` s ` of morphims indexed by a set ` i ` which all
       have the same domain ` d ` .  Joy of Cats, def. 10.1, p. 169.
       Experimental. $)
    df-source $a |- Source = ( c e. Cat , i e. _V |->
     { s e. ( dom ( dom_ ` c ) ^m i ) |
        A. x e. i A. y e. i ( ( dom_ ` c ) ` ( s ` x )
      ) = ( ( dom_ ` c ) ` ( s ` y ) ) } ) $.
  $}

  ${
    $d c i s x y I $.  $d c i s x y T $.  $d s x y F $.
    propsrc.1 $e |- M = dom ( dom_ ` T ) $.
    propsrc.2 $e |- D = ( dom_ ` T ) $.
    propsrc.4 $e |- S = ( T Source I ) $.
    $( Properties of a source.  (Contributed by FL, 30-May-2014.) $)
    propsrc $p |- ( ( T e. Cat /\ I e. A /\ F e. S )
      -> ( F : I --> M /\ A. x e. I A. y e. I
     ( D ` ( F ` x ) ) = ( D ` ( F ` y ) ) ) ) $=
      ( vs wcel cfv wceq wral wa cvv wi vc vi ccat wf cv csrce eleq2i cdom_ cdm
      co elex cmap simpl fveq2d dmeqd simpr oveq12d eqeq12d raleqbidv rabeqbidv
      crab fveq1d df-source ovex rabex eleq2d fveq1 2ralbidv elrab oveq1 eqcoms
      ovmpt2a biimpd fvex dmex jctil eleq1 anbi1d syl5ibr ax-mp elmapg syl syl9
      wb eqcomi fveq1i eqeq12i 2ralbii biimpi a1i anim12d com12 sylbi syl6bi ex
      pm2.43a syl5 syl7bi 3imp ) FUCNZHCNZGENZHIGUDZAUEZGOZDOZBUEZGOZDOZPZBHQAH
      QZRZXBGFHUFUJZNZWTXAXLEXMGLUGXAHSNZWTXNXLTZHCUKWTXOXPXNWTXORZXLXQXNGXDMUE
      ZOZFUHOZOZXGXROZXTOZPZBHQZAHQZMXTUIZHULUJZVAZNZXQXLTZXQXMYIGUAUBFHUCSXSUA
      UEZUHOZOZYBYMOZPZBUBUEZQZAYQQZMYMUIZYQULUJZVAYIUFYLFPZYQHPZRZYSYFMUUAYHUU
      DYTYGYQHULUUDYMXTUUDYLFUHUUBUUCUMUNZUOUUBUUCUPZUQUUDYRYEAYQHUUFUUDYPYDBYQ
      HUUFUUDYNYAYOYCUUDXSYMXTUUEVBUUDYBYMXTUUEVBURUSUSUTABUBMUAVCYFMYHYGHULVDV
      EVLVFYJGYHNZXEXTOZXHXTOZPZBHQAHQZRZYKYFUUKMGYHXRGPZYDUUJABHHUUMYAUUHYCUUI
      UUMXSXEXTXDXRGVGUNUUMYBXHXTXGXRGVGUNURVHVIXQUULXLXQUUGXCUUKXKIYGPZXQUUGXC
      TTJUUNUUGGIHULUJZNZXQXCUUNUUGUUPUUNYHUUOGYHUUOPYGIYGIHULVJVKVFVMXQUUPXCXQ
      ISNZXORZUUPXCWDUUNXQUURTJXQUURUUNYGSNZXORXQXOUUSWTXOUPXTFUHVNVOVPUUNUUQUU
      SXOIYGSVQVRVSVTIHGSSWAWBVMWCVTUUKXKTXQUUKXKUUJXJABHHUUHXFUUIXIXEXTDDXTKWE
      ZWFXHXTDUUTWFWGWHWIWJWKWLWMWNWPWOWQWRWS $.
      $( [30-May-2014] $)
  $}

  $c Sink $.

  $( Extend class notation with the class of all sinks. $)
  csnk $a class Sink $.

  ${
    $d c i f x y $.
    $( A sink is a family of morphims indexed by a set ` x ` which all have the
       same codomain.  Joy of Cats, def. 10.62, p. 184.  Experimental. $)
    df-sink $a |- Sink = ( c e. Cat , i e. _V |-> { f | ( f : i --> dom
      ( dom_ ` c ) /\ A. x e. i A. y e. i ( ( cod_ ` c ) ` ( f ` x ) ) =
      ( ( cod_ ` c ) ` ( f ` y ) ) ) } ) $.
  $}

  $c Natural $.

  $( Extend class notation with the class of all natural sources. $)
  cntrl $a class Natural $.

  ${
    $d i c d s m $.
    $( Let ` d e. ( Func `` <. i , c >. ) ` be a diagram, a source indexed by
       the objects of ` i ` is said to be natural for the diagram provided that
       each morphism ` m ` of ` i ` and the morphisms of the source connected
       to the domain and codomain of ` m ` commute.  Let's recall that a
       diagram is simply a functor ` ( Func `` <. i , c >. ) ` except one uses
       it as an indexed family of objects or morphisms of ` c ` .  Joy of Cats,
       def 11.3, (1) p. 193.  Experimental. $)
    df-natur $a |- Natural = ( i e. Cat , c e. Cat |-> ( d e. ( Func `
      <. i , c >. ) |-> { s e. ( Source ` <. c , dom ( id_ ` i ) >. ) |
      A. m e. dom ( dom_ ` i ) ( ( s ` ( ( dom_ ` i ) ` m ) ) ( o_ ` c ) (
      d ` m ) ) = ( s ` ( ( cod_ ` i ) ` m ) ) } ) ) $.
  $}

