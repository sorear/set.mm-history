$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Fields and Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d H g h $.  $d R a b g h $.  $d X a b g h $.
    com2i.1 $e |- G = ( 1st ` R ) $.
    com2i.2 $e |- H = ( 2nd ` R ) $.
    com2i.3 $e |- X = ran G $.
    $( Property of a commutative structure with two operation.  (Contributed by
       FL, 14-Feb-2010.) $)
    com2i $p |- ( R e. Com2 -> A. a e. X A. b e. X ( a H b ) = ( b H a ) ) $=
      ( vh vg ccm2 wcel cv co wceq crn wral cfv sylbi copab df-com2 eleq2i c1st
      wb eqcomi eqeq2i rneq syl6eqr raleq raleqbi1dv c2nd oveq eqeq12d 2ralbidv
      syl elopabi ) ALMAENZFNZJNZOZUSURUTOZPZFKNZQZRZEVERZKJUAZMURUSCOZUSURCOZP
      ZFDREDRZLVHAKJEFUBUCVGVCFDRZEDRZVLKJAVDAUDSZPVDBPZVGVNUEZVOBVDBVOGUFUGVPV
      EDPVQVPVEBQDVDBUHIUIVFVMEVEDVCFVEDUJUKUPTUTAULSZPZVCVKEFDDVSUTCPZVCVKUEVR
      CUTCVRHUFUGVTVAVIVBVJURUSUTCUMUSURUTCUMUNTUOUQT $.
      $( [18-Feb-2010] $) $( [14-Feb-2010] $)
  $}

  ${
    $d X u $.
    $( The domain of the first variable of an internal operation with a left
       and right identity element equals its base set.  (Contributed by FL,
       24-Jan-2010.) $)
    rngmgmbs3 $p |- ( ( G : ( X X. X ) --> X /\
      E. u e. X A. x e. X ( ( x G u ) = x /\ ( u G x ) = x ) )
         -> dom dom G = X ) $=
      ( cv co wceq wa wral wrex cxp wf cdm c0 wne rexn0 wi fdm dmeq dmxp syl2im
      eqtr ex syl syl5com impcom ) AEZBEZCFUGGUHUGCFUGGHADIZBDJZDDKZDCLZCMZMZDG
      ZUJDNOZULUOUIBDPULUMUKGZUPUOQUKDCRUQUNUKMZGZUPURDGZUOUMUKSDDTUSUTUOUNURDU
      BUCUAUDUEUF $.
      $( [24-Jan-2010] $)
  $}

  ${
    mlrngmgex.1 $e |- H = ( 2nd ` R ) $.
    $( In a unital ring the range of the multiplication equals the domain of
       the first variable.  (Contributed by FL, 24-Jan-2010.) $)
    rngodmdmrn $p |- ( R e. RingOps -> dom dom H = ran H ) $=
      ( crngo wcel c1st cfv crn cdm eqid rngorn1 rngorn1eq eqtr3d ) ADEAFGZHBII
      BHANBCNJZKANBCOLM $.
      $( [24-Jan-2010] $)
  $}

  ${
    rnplrnml3.1 $e |- G = ( 1st ` R ) $.
    $( In a unital ring the domain of the first operand of the addition equals
       the domain of the second operand of the addition.  (Contributed by FL,
       11-Feb-2010.) $)
    rngodmeqrn $p |- ( R e. RingOps -> dom dom G = ran dom G ) $=
      ( crngo wcel cgr cdm crn wceq rngogrpo cxp wfo wf wi eqid grpofo fof eqtr
      c0 wne fdm grpon0 dmeq dmxpid syl6eq rneq rnxp syl5com syl5 expcom eqcoms
      ex syl6 com13 mpcom syl2im 3syl sylc ) ADEBFEZUSBGZGZUTHZIZABCJZVDUSBHZVE
      KZVEBLVFVEBMZUSVCNBVEVEOZPVFVEBQVGUTVFIZUSVESTZVCVFVEBUABVEVHUBVAVEIZVIVJ
      VCNVIVAVFGVEUTVFUCVEUDUEVJVIVKVCVJVIVBVEIZVKVCNZVIVBVFHZIZVJVLUTVFUFVJVNV
      EIZVOVLVEVEUGVOVPVLVBVNVERULUHUIVMVEVBVKVEVBIVCVAVEVBRUJUKUMUNUOUPUQUR $.
      $( [18-Feb-2010] $) $( [11-Feb-2010] $)
  $}

  ${
    ununr.1 $e |- H = ( 2nd ` R ) $.
    ununr.2 $e |- X = ran ( 1st ` R ) $.
    $d H x y $.  $d X x y $.
    $( The unit of a unital ring is unique.  (Contributed by FL, 12-Jul-2009.)
       (The proof was shortened by Mario Carneiro, 23-Dec-2013.) $)
    ununr $p |- ( R e. RingOps -> E! x e. X
        A. y e. X ( ( x H y ) = y /\ ( y H x ) = y ) ) $=
      ( crngo wcel cv co wceq wa crn wral wreu cmnd cmagm cexid eqid cin exidu1
      rngomnd mndmgmid 3syl c1st cfv rngorn1eq syl5req raleq reueqd syl mpbid
      wb ) CHIZAJZBJZDKUQLUQUPDKUQLMZBDNZOZAUSPZURBEOZAEPZUODQIDRSUAIVACDFUCDUD
      BADUSUSTUBUEUOUSELVAVCUNUOECUFUGZNUSGCVDDFVDTUHUIUTVBAUSEURBUSEUJUKULUM
      $.
      $( [23-Dec-2013] $) $( [12-Jul-2009] $)
  $}

  ${
    rnginvcl.1 $e |- X = ran G $.
    rnginvcl.2 $e |- N = ( inv ` G ) $.
    $( The additive inverse of a unital ring element pertains to the unital
       ring.  (Contributed by FL, 18-Apr-2010.) $)
    rngoinvcl $p |- ( ( <. G , H >. e. RingOps /\ A e. X )
         -> ( N ` A ) e. X ) $=
      ( cop crngo wcel cfv cabl cgr wi rngoablo2 ablogrpo grpoinvcl ex 3syl imp
      ) BCHIJZAEJZADKEJZUABLJBMJZUBUCNBCOBPUDUBUCABDEFGQRST $.
      $( [18-Apr-2010] $)
  $}

  ${
    multinv.1 $e |- X = ran G $.
    multinv.2 $e |- G = ( 1st ` R ) $.
    multinv.3 $e |- H = ( 2nd ` R ) $.
    $( Multiplication by an additive inverse.  (Contributed by FL,
       2-Sep-2009.) $)
    multinv $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
    -> ( ( ( inv ` G ) ` A ) H B ) = ( ( inv ` G ) ` ( A H B ) ) ) $=
      ( crngo wcel w3a co cgn cfv wceq cgi eqid eqcomd rngocl simp1 wi rngogrpo
      simp2 cgr grpoinvcl ex a1dd 3imp simp3 rngodir syl13anc 3ad2ant1 grporinv
      syl wa syl2anc oveq1d rngolz 3adant2 3eqtrd wb syld3an2 grpoinvid1 mpbird
      syl3anc ) CJKZAFKZBFKZLZABEMZDNOZOZAVLOZBEMZVJVMVOPZVKVODMZDQOZPZVJVQAVND
      MZBEMZVRBEMZVRVJVGVHVNFKZVIVQWAPVGVHVIUAVGVHVIUDZVGVHVIWCVGVHWCVIVGDUEKZV
      HWCUBCDHUCZWEVHWCADVLFGVLRZUFUGUOUHUIZVGVHVIUJVGVHWCVILUPWAVQAVNBCDEFHIGU
      KSULVJVTVRBEVJWEVHVTVRPVGVHWEVIWFUMZWDAVRDVLFGVRRZWGUNUQURVGVIWBVRPVHBCDE
      FVRWJGHIUSUTVAVJWEVKFKVOFKZVPVSVBWIABCDEFHIGTVGWCVHVIWKWHVNBCDEFHIGTVCVKV
      OVRDVLFGWJWGVDVFVES $.
      $( [2-Sep-2009] $)

    $( Multiplication by an additive inverse.  (Contributed by FL,
       6-Sep-2009.) $)
    multinvb $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
     -> ( A H ( ( inv ` G ) ` B ) ) = ( ( inv ` G ) ` ( A H B ) ) ) $=
      ( crngo wcel w3a co cgn cfv wceq cgi cgr eqid rngocl simp3 syl2anc oveq2d
      rngogrpo 3ad2ant1 grporinv simp1 simp2 wi grpoinvcl syl a1d 3imp syl13anc
      rngodi rngorz 3adant3 3eqtr3d syld3an3 grpoinvid1 syl3anc mpbird eqcomd
      ex wb ) CJKZAFKZBFKZLZABEMZDNOZOZABVKOZEMZVIVLVNPZVJVNDMZDQOZPZVIABVMDMZE
      MZAVQEMZVPVQVIVSVQAEVIDRKZVHVSVQPVFVGWBVHCDHUDZUEZVFVGVHUAZBVQDVKFGVQSZVK
      SZUFUBUCVIVFVGVHVMFKZVTVPPVFVGVHUGVFVGVHUHWEVFVGVHWHVFVHWHUIZVGVFWBWIWCWB
      VHWHBDVKFGWGUJVDUKULUMZABVMCDEFHIGUOUNVFVGWAVQPVHACDEFVQWFGHIUPUQURVIWBVJ
      FKVNFKZVOVRVEWDABCDEFHIGTVFVGVHWHWKWJAVMCDEFHIGTUSVJVNVQDVKFGWFWGUTVAVBVC
      $.
      $( [6-Sep-2009] $)
  $}

  ${
    mult2inv.1 $e |- X = ran G $.
    mult2inv.2 $e |- G = ( 1st ` R ) $.
    mult2inv.3 $e |- H = ( 2nd ` R ) $.
    $( Multiplication of two additive inverses.  (Contributed by FL,
       6-Sep-2009.) $)
    mult2inv $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
     -> ( ( ( inv ` G ) ` A ) H ( ( inv ` G ) ` B ) ) = ( A H B ) ) $=
      ( crngo wcel w3a cgn cfv co wceq cgr rngogrpo 3ad2ant1 syl2anc simp3 eqid
      grpoinvcl multinv syld3an3 multinvb fveq2d rngocl grpo2inv 3eqtrd ) CJKZA
      FKZBFKZLZADMNZNBUONZEOZAUPEOZUONZABEOZUONZUONZUTUKULUMUPFKZUQUSPUNDQKZUMV
      CUKULVDUMCDHRSZUKULUMUABDUOFGUOUBZUCTAUPCDEFGHIUDUEUNURVAUOABCDEFGHIUFUGU
      NVDUTFKVBUTPVEABCDEFHIGUHUTDUOFGVFUITUJ $.
      $( [6-Sep-2009] $)
  $}

  ${
    $d H u x $.  $d X u x $.  $d R u $.
    rngunval2.1 $e |- X = ran ( 1st ` R ) $.
    rngunval2.2 $e |- H = ( 2nd ` R ) $.
    rngunval2.3 $e |- U = ( Id ` H ) $.
    $( The value of the unit of a ring.  (Contributed by FL, 12-Feb-2010.)
       (Revised by Mario Carneiro, 23-Dec-2013.) $)
    rngounval2 $p |- ( R e. RingOps -> U = ( iota_ u e. X
     A. x e. X ( ( u H x ) = x /\ ( x H u ) = x ) ) ) $=
      ( wcel cv co wceq wral crio crn cfv eqid cvv c2nd crngo wa c1st rngorn1eq
      syl5eq raleqdv riotaeqbidv cgi fvex eqeltri gidval ax-mp eqtri syl6reqr )
      CUAJZBKZAKZELUQMUQUPELUQMUBZAFNZBFOURAEPZNZBUTOZDUOUSVABFUTUOFCUCQZPUTGCV
      CEHVCRUDUEZUOURAFUTVDUFUGDEUHQZVBIESJVEVBMECTQSHCTUIUJABESUTUTRUKULUMUN
      $.
      $( [23-Dec-2013] $) $( [12-Feb-2010] $)
  $}

  ${
    $d G x y $.  $d H x y $.
    $( The predicate "is a field".  (Contributed by FL, 6-Sep-2009.) $)
    isfld $p |- ( ( G e. A /\ H e. B ) -> ( <. G , H >. e. Fld <->
      ( <. G , H >. e. DivRingOps
      /\ A. x e. ran G A. y e. ran G ( x H y ) = ( y H x ) ) ) ) $=
      ( wcel wa cop cfld cdrng ccm2 cin cv co wceq crn wral df-fld a1i wb elin
      eleq2d iscom2 anbi2d 3bitrd ) ECGFDGHZEFIZJGUHKLMZGZUHKGZUHLGZHZUKANZBNZF
      OUOUNFOPBEQZRAUPRZHUGJUIUHJUIPUGSTUCUJUMUAUGUHKLUBTUGULUQUKCDEFABUDUEUF
      $.
      $( [6-Sep-2009] $)
  $}

  ${
    fldi.1 $e |- G = ( 1st ` R ) $.
    fldi.2 $e |- H = ( 2nd ` R ) $.
    fldi.3 $e |- X = ran G $.
    fldi.4 $e |- Z = ( Id ` G ) $.
    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( The "axioms" of a field.  (Contributed by FL, 15-Sep-2010.) $)
      fldi $p |- ( R e. Fld ->
         (
              ( G e. AbelOp /\ H : ( X X. X ) --> X )
              /\ (
                 A. x e. X A. y e. X A. z e. X (
                         ( ( x H y ) H z ) = ( x H ( y H z ) )
                        /\ ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) )
                        /\ ( ( x G y ) H z ) = ( ( x H z ) G ( y H z ) ) )
                 /\ E. x e. X A. y e. X (
                         ( x H y ) = y
                        /\ ( y H x ) = y ) )
               /\ (
                  ( H |` ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. GrpOp
                 /\ A. x e. X A. y e. X ( x H y ) = ( y H x ) ) ) ) $=
        ( wcel cdrng ccm2 wa cv co wceq wral cfld cin cabl cxp wf w3a wrex cdif
        csn cres cgr df-fld eleq2i elin crngo drngoi com2i rngoi 3expia expdimp
        wi id syl syl2im imp sylbi ) DUAMDNOUBZMZEUCMGGUDGFUEPZAQZBQZFRZCQZFRVJ
        VKVMFRZFRSVJVKVMERFRVLVJVMFRZERSVJVKERVMFRVOVNERSUFCGTBGTAGTVLVKSVKVJFR
        ZVKSPBGTAGUGPZFGHUIUHZVRUDUJUKMZVLVPSBGTAGTZPZUFZUAVGDULUMVHDNMZDOMZPWB
        DNOUNWCWDWBWCDUOMZVSPWDVTWBDEFGHIJKLUPDEFGABIJKUQWEVSVTWBWEVIVQPWAWBVAA
        BCDEFGIJKURVIVQWAWBWBVBUSVCUTVDVEVFVF $.
        $( [23-Dec-2013] $) $( [15-Sep-2010] $)
    $}
  $}

  ${
    $d G x y z $.  $d R x y z $.
    fldax1.1 $e |- G = ( 1st ` R ) $.
    $( 1st "axiom" of a field.  The addition is an abelian group.  (Contributed
       by FL, 11-Jul-2010.) $)
    fldax1 $p |- ( R e. Fld -> G e. AbelOp ) $=
      ( vx vy vz cfld wcel cabl crn cxp c2nd cfv wf wa cv co wceq wral eqid w3a
      wrex cgi csn cdif cres cgr fldi simp1d simpld ) AGHZBIHZBJZUMKUMALMZNZUKU
      LUOODPZEPZUNQZFPZUNQUPUQUSUNQZUNQRUPUQUSBQUNQURUPUSUNQZBQRUPUQBQUSUNQVAUT
      BQRUAFUMSEUMSDUMSURUQRUQUPUNQZUQROEUMSDUMUBOUNUMBUCMZUDUEZVDKUFUGHURVBREU
      MSDUMSODEFABUNUMVCCUNTUMTVCTUHUIUJ $.
      $( [23-Dec-2013] $) $( [11-Jul-2010] $)
  $}

  ${
    fldax.1 $e |- G = ( 1st ` R ) $.
    fldax.2 $e |- H = ( 2nd ` R ) $.
    fldax.3 $e |- X = ran G $.

    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 2nd "axiom" of a field.  The multiplication is an internal operation.
         (Contributed by FL, 11-Jul-2010.) $)
      fldax2 $p |- ( R e. Fld -> H : ( X X. X ) --> X ) $=
        ( vx vy vz cfld wcel cabl cxp wf wa cv co wceq wral w3a cgi cfv csn cgr
        wrex cdif cres eqid fldi simp1d simprd ) AKLZBMLZDDNDCOZUMUNUOPHQZIQZCR
        ZJQZCRUPUQUSCRZCRSUPUQUSBRCRURUPUSCRZBRSUPUQBRUSCRVAUTBRSUAJDTIDTHDTURU
        QSUQUPCRZUQSPIDTHDUFPCDBUBUCZUDUGZVDNUHUELURVBSIDTHDTPHIJABCDVCEFGVCUIU
        JUKUL $.
        $( [23-Dec-2013] $) $( [11-Jul-2010] $)
    $}

    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 3rd "axiom" of a field.  The multiplication is associative.
         (Contributed by FL, 11-Jul-2010.) $)
      fldax3 $p |- ( R e. Fld -> A. x e. X A. y e. X A. z e. X
                         ( ( x H y ) H z ) = ( x H ( y H z ) ) ) $=
        ( cfld wcel cv co wceq w3a wral wa cxp ralimi wrex cabl wf cgi cfv cdif
        csn cres cgr eqid fldi simp2d simp1 adantr syl ) DKLZAMZBMZFNZCMZFNUQUR
        UTFNZFNOZUQURUTENFNUSUQUTFNZENOZUQURENUTFNVCVAENOZPZCGQZBGQZAGQZUSUROUR
        UQFNZURORBGQAGUAZRZVBCGQZBGQZAGQZUPEUBLGGSGFUCRVLFGEUDUEZUGUFZVQSUHUILU
        SVJOBGQAGQRABCDEFGVPHIJVPUJUKULVIVOVKVHVNAGVGVMBGVFVBCGVBVDVEUMTTTUNUO
        $.
        $( [23-Dec-2013] $) $( [11-Jul-2010] $)
    $}


    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 4th "axiom" of a field.  The multiplication is distributive.
         (Contributed by FL, 11-Jul-2010.) $)
      fldax4 $p |- ( R e. Fld -> A. x e. X A. y e. X A. z e. X
                        ( x H ( y G z ) ) = ( ( x H y ) G ( x H z ) ) ) $=
        ( cfld wcel cv co wceq w3a wral wa cxp ralimi wrex cabl wf cgi cfv cdif
        csn cres cgr eqid fldi simp2d simp2 adantr syl ) DKLZAMZBMZFNZCMZFNUQUR
        UTFNZFNOZUQURUTENFNUSUQUTFNZENOZUQURENUTFNVCVAENOZPZCGQZBGQZAGQZUSUROUR
        UQFNZURORBGQAGUAZRZVDCGQZBGQZAGQZUPEUBLGGSGFUCRVLFGEUDUEZUGUFZVQSUHUILU
        SVJOBGQAGQRABCDEFGVPHIJVPUJUKULVIVOVKVHVNAGVGVMBGVFVDCGVBVDVEUMTTTUNUO
        $.
        $( [23-Dec-2013] $) $( [11-Jul-2010] $)
    $}


    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 5th "axiom" of a field.  Existence of a neutral element.  (Contributed
         by FL, 11-Jul-2010.) $)
      fldax5 $p |- ( R e. Fld -> E. x e. X A. y e. X ( y H x ) = y ) $=
        ( vz cfld wcel cv co wceq wa wral wrex w3a cxp cabl wf cgi cfv csn cdif
        cres cgr eqid fldi simp2d simprd simpr ralimi reximi syl ) CKLZAMZBMZEN
        ZUSOZUSURENZUSOZPZBFQZAFRZVCBFQZAFRUQUTJMZENURUSVHENZENOURUSVHDNENUTURV
        HENZDNOURUSDNVHENVJVIDNOSJFQBFQAFQZVFUQDUALFFTFEUBPVKVFPEFDUCUDZUEUFZVM
        TUGUHLUTVBOBFQAFQPABJCDEFVLGHIVLUIUJUKULVEVGAFVDVCBFVAVCUMUNUOUP $.
        $( [23-Dec-2013] $) $( [11-Jul-2010] $)
    $}

    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      fldax.4 $e |- Z = ( Id ` G ) $.
      $( 6th "axiom" of a field.  The multiplication is a group on the
         underlying set deprived from zero.  (Contributed by FL,
         11-Jul-2010.) $)
      fldax6 $p |- ( R e. Fld -> ( H |`
        ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. GrpOp ) $=
        ( vx vy vz cfld wcel cxp cv co wceq wral wa csn cdif cres cgr cabl wrex
        wf w3a fldi simp3d simpld ) AMNZCDEUAUBZUMOUCUDNZJPZKPZCQZUPUOCQZRKDSJD
        SZULBUENDDODCUGTUQLPZCQUOUPUTCQZCQRUOUPUTBQCQUQUOUTCQZBQRUOUPBQUTCQVBVA
        BQRUHLDSKDSJDSUQUPRURUPRTKDSJDUFTUNUSTJKLABCDEFGHIUIUJUK $.
        $( [23-Dec-2013] $) $( [11-Jul-2010] $)
    $}

    ${
      $d G x y z $.  $d H x y z $.  $d R x y $.  $d X x y z $.
      $( 7th "axiom" of a field.  The multiplication is commutative.
         (Contributed by FL, 11-Jul-2010.) $)
      fldax7 $p |- ( R e. Fld
        -> A. x e. X A. y e. X ( x H y ) = ( y H x ) ) $=
        ( vz cfld wcel cgi cfv cxp cv co wceq wral wa csn cdif cres cgr cabl wf
        w3a wrex eqid fldi simp3d simprd ) CKLZEFDMNZUAUBZUOOUCUDLZAPZBPZEQZURU
        QEQZRBFSAFSZUMDUELFFOFEUFTUSJPZEQUQURVBEQZEQRUQURVBDQEQUSUQVBEQZDQRUQUR
        DQVBEQVDVCDQRUGJFSBFSAFSUSURRUTURRTBFSAFUHTUPVATABJCDEFUNGHIUNUIUJUKUL
        $.
        $( [23-Dec-2013] $) $( [11-Jul-2010] $)
    $}

  $}

  ${
    zrfld.1 $e |- A e. _V $.
    $( The zero ring is not a field.  (Contributed by FL, 24-Jan-2010.)
       (Revised by Mario Carneiro, 15-Dec-2013.) $)
    zrfld $p |- -. <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >.
      e. Fld $=
      ( cop csn cfld wcel cdrng zrdivrng flddivrng mto ) AACACDZKCZEFLGFABHLIJ
      $.
      $( [15-Dec-2013] $) $( [24-Jan-2010] $)
  $}

  ${
    zerdivemp.1 $e |- G = ( 1st ` R ) $.
    zerdivemp.2 $e |- H = ( 2nd ` R ) $.
    zerdivemp.3 $e |- Z = ( Id ` G ) $.
    zerdivemp.4 $e |- X = ran G $.
    zerdivemp.5 $e |- U = ( Id ` H ) $.
    ${
      $d A a $.  $d B a $.  $d H a $.  $d R a $.  $d X a $.  $d Z a $.
      $( In a unitary ring a left invertible element is not a zero divisor.
         (Contributed by FL, 18-Apr-2010.) $)
      zerdivemp1 $p |- ( ( R e. RingOps /\ A e. ( X \ Z ) /\
      E. a e. X ( a H A ) = U ) ->
         ( B e. X -> ( ( A H B ) = Z -> B = Z ) ) ) $=
        ( wcel co wceq wi w3a wa crngo cdif oveq2 simpl1 simpr1 eldifi 3ad2ant3
        cv wrex adantl simpl3 3jca jca rngoass eqtr ex oveq1 rngorz 3adant3 crn
        c1st rneqi eqtri rngolidm 3adant2 simp1 simp2 simp3 3eqtr3d com14 com13
        cfv a1d 3exp sylc com15 com24 syl eqcoms com25 com12 3imp 3imp1 pm2.43i
        syl6 3syl 3exp1 syl5com rexlimiv ) CUAOZAGHUBOZIUHZAFPZDQZIGUIZBGOZABFP
        ZHQZBHQZRRZWOWKWJWTWNWKWJWTRZRIGWLGOZWNWKXAWRWJWPXBWNWKSZWSWRWLWQFPZWLH
        FPZQZWJWPXCWSRRWQHWLFUCWJXFWPXCWSWJXFWPSZXCTZWSXHWJXBAGOZWPSZTWMBFPZXDQ
        ZXHWSRXHWJXJWJXFWPXCUDXHXBXIWPXGXBWNWKUEXCXIXGWKXBXIWNAGHUFUGUJWJXFWPXC
        UKULUMWLABCEFGJKMUNXHXLWSWJXFWPXCXLWSRXLXFWPXCWJWSXLXFXKXEQZWPXCWJWSRZR
        RXLXFXMXKXDXEUOUPXCWPXMXNXBWNWKWPXMXNRRZWNXBWKXORZWNXKDBFPZQZXBXPRWMDBF
        UQXRXMWKWPXBXNXMWKWPXBXNRRRZRXQXKXQXKQZXMXSXTXMTXQXEQZXSXQXKXEUOYAXBWPW
        KXNWJXBWPWKYAWSWJXBWPWKYAWSRZRZWJXBWPSXEHQZXQBQZYCWJXBYDWPWLCEFGHLMJKUR
        USWJWPYEXBBCDFGKGEUTCVAVLZUTMEYFJVBVCNVDVEWKYEYDYBYAYEYDWKWSYAYEYDWKWSR
        YAYEYDSZWSWKYGXQXEBHYAYEYDVFYAYEYDVGYAYEYDVHVIVMVNVJVKVOVNVPVQVRUPVSVTV
        RWAWBVKWEVPWCWAWFWDWGWHVJVNWIVKWB $.
        $( [18-Apr-2010] $)
    $}

    ${
      $d A a $.  $d R a $.  $d U a $.  $d X a $.  $d Z a $.
      $( In a unitary ring a left invertible element is different from zero iff
         ` 1 =/= 0 ` .  (Contributed by FL, 18-Apr-2010.) $)
      rngoridfz $p |- ( ( R e. RingOps /\ A e. X /\
         E. a e. X ( a H A ) = U ) -> ( A =/= Z <-> U =/= Z ) ) $=
        ( wcel co wceq w3a wi wa imp crngo cv wrex rngorz eqeq12 biimpd syl5com
        wb oveq2 ex com3l 3adant3 syl5 crn c1st cfv rneqi eqtri rngoridm expcom
        syl2anc 3ad2ant3 impbid 3exp1 rexlimiv com13 3imp necon3bid ) BUANZAFNZ
        HUBZAEOZCPZHFUCZQAGCGVIVJVNAGPZCGPZUHZVNVJVIVQVMVJVIVQRRHFVKFNZVMVJVIVQ
        VRVMVJQZVISZVOVPVOVLVKGEOZPZVTVPAGVKEUIVSVIWBVPRZVRVMVIWCRZVJVRVMWDVIVR
        VMWCVIVRVMWCRVIVRSWAGPZVMWCVKBDEFGKLIJUDVMWEWCVMWESWBVPVLCWAGUEUFUJUGUJ
        UKTULTUMVPACEOZAGEOZPZVTVOCGAEUIVSVIWHVORZVJVRVIWIRVMVIVJWIVIVJSWFAPZWG
        GPZWIABCEFJFDUNBUOUPZUNLDWLIUQURMUSABDEFGKLIJUDWJWKSWHVOWFAWGGUEUFVAUTV
        BTUMVCVDVEVFVGVH $.
        $( [18-Apr-2010] $)
    $}
  $}

  ${
    $d x y $.
    $( ` ZZ ` is a commutative ring.  (Contributed by FL, 18-Apr-2010.) $)
    zintdom $p |- <. + , x. >. e. ( RingOps i^i Com2 ) $=
      ( vx vy caddc cmul crngo ccm2 cin wcel cv co wceq wral cc wi eqcoms ax-mp
      cdm 3syl eleq2i cvv cop elin crn cxp wfo cabl cnaddablo cgr cmnd ablogrpo
      cnrngo grpomnd cmagm cexid mndmgmid ax-addopr fdm dmeq dmxpid eqtr expcom
      wf com12 opidon forn ax-mulcom syl2anb rgen2a wb addex mulex iscom2 mp2an
      syl mpbir mpbir2an ) CDUAZEFGHVQEHVQFHZVQEFUBUKVRAIZBIZDJVTVSDJKZBCUCZLAW
      BLZWAABWBVSWBHVSMHVTMHWAVTWBHWBMVSMMUDZMCUEZWBMKCUFHZWEUGWFCUHHCUIHZWECUJ
      CULWGCUMUNGHWECUOCMWDMCVBZMCQZQZKZUPWHWIWDKWJWDQZKZWKWDMCUQWIWDURWLMKWMWK
      NZMUSWNMWLWMMWLKZWKWOWKNWLWJWOWLWJKWKMWLWJUTVAOVCOPRPVDVNRPWDMCVEPZSWBMVT
      WPSVSVTVFVGVHCTHDTHVRWCVIVJVKTTCDABVLVMVOVP $.
      $( [18-Apr-2010] $)
  $}

  $c Tofld $.

  $( Extend class notation with the class of all totally ordered fields. $)
  ctofld $a class Tofld $.

  ${
    $d g h r x y z $.
    $( Definition of a totally ordered field.  Experimental. $)
    df-tofld $a |- Tofld = { <. <. g , h >. , r >. | ( <. g , h >. e. Fld
     /\ ( r e. TosetRel /\ U. U. r = ran g ) /\ A. x e. U. U. r A. y e. U. U. r
        A. z e. U. U. r ( ( x r y -> ( x g z ) r ( y g z ) ) /\
        ( ( Id ` g ) r z -> ( x h z ) r ( y h z ) ) ) ) } $.
  $}

  $c zeroDiv $.

  $( Extend class notation with the class of all the zero divisors. $)
  czerodiv $a class zeroDiv $.

  ${
    $d r y a b $.
    $( Definition of the zero divisors of a ring.  Experimental. $)
    df-zd $a |- zeroDiv = { <. r , y >. | y = { a e. ran ( 1st ` r ) |
        ( a =/= ( Id ` ( 1st ` r ) ) /\
          E. b e. ran ( 1st ` r )
             ( b =/= ( Id ` ( 1st ` r ) ) /\
             ( a ( 2nd ` r ) b ) = ( Id ` ( 1st ` r ) ) ) ) } } $.
  $}


