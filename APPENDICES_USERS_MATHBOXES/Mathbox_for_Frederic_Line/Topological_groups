$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topological groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TopGrp $.

  $( Extend class notation with the class of all topological groups. $)
  ctopgrp $a class TopGrp $.

  ${
    $d g j $.
    $( Define the class of all topological groups.  A topological group is a
       group whose operation and inverse function are continuous. $)
    df-topgrp $a |- TopGrp = { <. g , j >. | ( ( g e. GrpOp /\ j e. Top ) /\
         g e. ( ( j tX j ) Cn j ) /\ ( inv ` g ) e. ( j Cn j ) ) } $.
  $}

  ${
    $d G g j $.  $d J g j $.  $d x y $.
    $( The predicate "is a topological group".  Bourbaki TG III.1 (Contributed
       by FL, 18-Apr-2010.) $)
    istopgrp $p |- ( J e. A -> ( <. G , J >. e. TopGrp <->
      ( ( G e. GrpOp /\ J e. Top ) /\ G e. ( ( J tX J ) Cn J )
        /\ ( inv ` G ) e. ( J Cn J ) ) ) ) $=
      ( vx vy vg vj wcel ctopgrp cgr ctop wa ctx co ccn cgn cfv w3a cv eleq1 id
      cop cvv wbr df-br wrel copab relopab df-topgrp releqi mpbir sylbir adantl
      brrelexi simpl jca elex adantr 3ad2ant1 eleq2i anbi1d fveq2 eleq1d anbi2d
      wceq 3anbi123d oveq12d eleq2d opelopabg syl5bb pm5.21nd ) CAHZBCUBZIHZBJH
      ZCKHZLZBCCMNZCONZHZBPQZCCONZHZRZBUCHZVLLZVLVNLWEVLVNWEVLVNBCIUDWEBCIUEBCI
      IUFDSZJHESZKHLWGWHWHMNWHONHWGPQWHWHONHRZDEUGZUFWIDEUHIWJDEUIUJUKUNULUMVLV
      NUOUPVLWDLWEVLWDWEVLVQVTWEWCVOWEVPBJUQURUSUMVLWDUOUPVNVMFSZJHZGSZKHZLZWKW
      MWMMNZWMONZHZWKPQZWMWMONZHZRZFGUGZHWFWDIXCVMFGUIUTXBVOWNLZBWQHZWAWTHZRWDF
      GBCUCAWKBVEZWOXDWRXEXAXFXGWLVOWNWKBJTVAWKBWQTXGWSWAWTWKBPVBVCVFWMCVEZXDVQ
      XEVTXFWCXHWNVPVOWMCKTVDXHWQVSBXHWPVRWMCOXHWMCWMCMXHUAZXIVGXIVGVHXHWTWBWAX
      HWMCWMCOXIXIVGVHVFVIVJVK $.
      $( [18-Apr-2010] $)
  $}

  ${
    topgrpi.1 $e |- G = ( 1st ` K ) $.
    ${
      topgrpi.2 $e |- J = ( 2nd ` K ) $.
      ${
        $d G g j $.  $d J g j $.  $d K g j $.  $d N g j $.
        topgrpi.3 $e |- N = ( inv ` G ) $.
        $( "Axioms" of a topological group.  (Contributed by FL,
           18-Apr-2010.) $)
        topgrpi $p |- ( K e. TopGrp -> ( ( G e. GrpOp /\ J e. Top ) /\
          G e. ( ( J tX J ) Cn J ) /\ N e. ( J Cn J ) ) ) $=
          ( vg vj wcel cgr ctop wa ctx co ccn cgn cfv w3a wceq ctopgrp cv copab
          df-topgrp eleq2i wb eqtr3 eqcomd eleq1 anbi1d fveq2 syl6eqr 3anbi123d
          c1st eleq1d syl mpan c2nd anbi2d oveq12 anidms mpancom eleq2d elopabi
          sylbi ) CUAJCHUBZKJZIUBZLJZMZVFVHVHNOZVHPOZJZVFQRZVHVHPOZJZSZHIUCZJAK
          JZBLJZMZABBNOZBPOZJZDBBPOZJZSZUAVRCHIUDUEVQVSVIMZAVLJZDVOJZSZWGHICACU
          NRZTZVFWLTZVQWKUFZEWMWNMZVFATZWOWPAVFAVFWLUGUHWQVJWHVMWIVPWJWQVGVSVIV
          FAKUIUJVFAVLUIWQVNDVOWQVNAQRDVFAQUKGULUOUMUPUQBCURRZTZVHWRTZWKWGUFZFW
          SWTMZVHBTZXAXBBVHBVHWRUGUHXCWHWAWIWDWJWFXCVIVTVSVHBLUIUSXCVLWCAVKWBTZ
          XCVLWCTXCXDVHBVHBNUTVAVKWBVHBPUTVBVCXCVOWEDXCVOWETVHBVHBPUTVAVCUMUPUQ
          VDVE $.
          $( [18-Apr-2010] $)

        $( In a topological group, the inverse function is continuous.
           (Contributed by FL, 21-Jun-2010.) $)
        topgrpinv $p |- ( K e. TopGrp -> N e. ( J Cn J ) ) $=
          ( ctopgrp wcel cgr ctop wa ctx co ccn topgrpi simp3d ) CHIAJIBKILABBM
          NBONIDBBONIABCDEFGPQ $.
          $( [21-Jun-2010] $)
      $}

      $( In a topological group, the base set of the topology and the base set
         of the group are equal.  (Contributed by FL, 21-Jun-2010.) $)
      topgrpbs $p |- ( K e. TopGrp -> ran G = U. J ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn w3a wceq eqid wi wf fdm syl eqtr2
        cgn cfv crn cuni topgrpi wf1o grpoinvf cnf f1of cdm syl2im syl5com 3exp
        ex pm2.43i com3r imp31 3adant2 ) CFGAHGZBIGZJZABBKLBMLGZAUBUCZBBMLGZNAU
        DZBUEZOZABCVDDEVDPZUFVBVEVHVCUTVAVEVHUTVFVFVDUGZVAVEVHQQAVDVFVFPVIUHVAV
        EVJVHVAVEVJVHQZQVAVAVEVKVAVAVENVGVGVDRZVJVHVDBBVGVGVGPZVMUIVJVFVFVDRZVL
        VHQVFVFVDUJVNVDUKZVFOZVLVOVGOZVHVFVFVDSVGVGVDSVPVQVHVOVFVGUAUOULTUMUNUP
        UQTURUST $.
        $( [21-Jun-2010] $)

      $( In a topological group, the operation ` G ` is continuous.
         (Contributed by FL, 21-Jun-2010.) $)
      topgrpcn $p |- ( K e. TopGrp -> G e. ( ( J tX J ) Cn J ) ) $=
        ( ctopgrp wcel cgr ctop wa ctx co ccn cgn cfv eqid topgrpi simp2d ) CFG
        AHGBIGJABBKLBMLGANOZBBMLGABCSDESPQR $.
        $( [21-Jun-2010] $)
    $}

    $( In a topological group, the operation ` G ` is a group.  (Contributed by
       FL, 21-Jun-2010.) $)
    topgrpgrp $p |- ( K e. TopGrp -> G e. GrpOp ) $=
      ( ctopgrp wcel cgr c2nd cfv ctop wa ctx co ccn eqid topgrpi simp1d simpld
      cgn ) BDEZAFEZBGHZIEZSTUBJAUAUAKLUAMLEARHZUAUAMLEAUABUCCUANUCNOPQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    topgrptop.1 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, ` J ` is a topology.  (Contributed by FL,
       21-Jun-2010.) $)
    topgrptop $p |- ( K e. TopGrp -> J e. Top ) $=
      ( ctopgrp wcel c1st cfv cgr ctop wa ctx co ccn eqid topgrpi simp1d simprd
      cgn ) BDEZBFGZHEZAIEZSUAUBJTAAKLAMLETRGZAAMLETABUCTNCUCNOPQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    extopgrp.1 $e |- A e. _V $.
    $( The trivial topological group.  (Contributed by FL, 21-Jun-2010.)
       (Revised by Mario Carneiro, 15-Dec-2013.) $)
    extopgrp $p |- <. { <. <. A , A >. , A >. } , { (/) , { A } } >.
       e. TopGrp $=
      ( cop csn c0 cpr ctopgrp wcel cgr wa ctx co ccn cgn cfv cvv w3a ax-mp cun
      ctop wb prex istopgrp grposn indistop pm3.2i cxp wf txtopi wfo crn rnsnop
      opex eqcomi grpofo fof mp2b cuni 0ex unipr uncom 3eqtrri txunii mapudiscn
      snex un0 mp2an cid cres ginvsn idcn eqeltri mpbir3an ) AACZACDZEADZFZCGHZ
      VOIHZVQTHZJZVOVQVQKLZVQMLHZVONOZVQVQMLZHZVQPHVRWAWCWFQUAEVPUBPVOVQUCRVSVT
      ABUDZVPUEZUFWBTHVPVPUGZVPVOUHZWCVQVQWHWHUIVSWIVPVOUJWJWGVOVPVOUKVPVNAAAUM
      BULUNUOWIVPVOUPUQWIVPVOWBVQVQVPVPWHWHVQUREVPSVPESVPEVPUSAVEZUTEVPVAVPVFVB
      ZWLVCWKVDVGWDVHVPVIZWEABVJVTWMWEHWHVQVPWLVKRVLVM $.
      $( [15-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    $d x y z G $.  $d x y z J $.  $d x y z K $.
    topgrpsubcn.1 $e |- G = ( 1st ` K ) $.
    topgrpsubcn.2 $e |- J = ( 2nd ` K ) $.
    topgrpsubcn.3 $e |- D = ( /g ` G ) $.
    $( In a topological group, the "subtraction" (or "division") is
       continuous.  Bourbaki TG III.1 axiom GT' (Contributed by FL,
       21-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       30-May-2014.) $)
    topgrpsubcn $p |- ( K e. TopGrp -> D e. ( ( J tX J ) Cn J ) ) $=
      ( vx vy ctopgrp wcel cuni cv cgn cfv co cmpt2 ctx wceq eqid ccn topgrpgrp
      crn cgr grpodivfval syl topgrpbs mpt2eq12 syl2anc eqtrd cnmpt1st cnmpt2nd
      topgrptop topgrpinv cnmpt21f topgrpcn cnmpt22f eqeltrd ) DJKZAHICLZUTHMZI
      MZBNOZOZBPZQZCCRPCUAPUSAHIBUCZVGVEQZVFUSBUDKAVHSBDEUBHIABVCVGVGTVCTZGUEUF
      USVGUTSZVJVHVFSBCDEFUGZVKHIVGVGUTUTVEUHUIUJUSHIVAVDBCCCCCUTUTUTUTUTTZVLCD
      FUMZVMVMVMUSHICCUTUTVLVLVMVMUKUSHIVBVCCCCCUTUTVLVLVMVMVMVMUSHICCUTUTVLVLV
      MVMULBCDVCEFVIUNUOVLVLVMBCDEFUPUQUR $.
      $( [23-Dec-2013] $) $( [21-Jun-2010] $)
  $}

  ${
    $d x y A $.  $d y F $.  $d x y G $.  $d x y J $.  $d x y K $.  $d x y X $.
    $d y O $.
    trhom.1 $e |- X = ran G $.
    trhom.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trhom.3 $e |- G = ( 1st ` K ) $.
    trhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group a right translation is a homeomorphism.  Bourbaki
       TG III.2 (Contributed by FL, 21-Jun-2010.)  (The proof was shortened by
       Mario Carneiro, 30-May-2014.) $)
    trhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
      ( wcel wa co ccn adantr cmpt wceq syl5eq mpteq1 ctopgrp ccnv wb topgrptop
      chomeosm ctop hmeobc syl2anc cuni crn topgrpbs eqid cnmptid simpr eleqtrd
      cv syl cnmptc ctx topgrpcn cnmpt12f eqeltrd cgs cfv topgrpgrp trinv sylan
      cgr eqtrd topgrpsubcn mpbir2and ) FUALZBGLZMZCEEUENLZCEEONZLZCUBZVPLZVNEU
      FLZVTVOVQVSMUCVLVTVMEFKUDPZWACEEUGUHVNCAEUIZAUPZBDNZQZVPVNCAGWDQZWEIVNGWB
      RZWFWERVNGDUJZWBHVLWHWBRVMDEFJKUKPSZAGWBWDTUQSVNAWCBDEEEEWBWBWBULZWAWJWAW
      AVNAEWBWJWAUMZVNABEEWBWBWJWAWJWAVNBGWBVLVMUNWIUOURZWAVLDEEUSNEONZLVMDEFJK
      UTPVAVBVNVRAWBWCBDVCVDZNZQZVPVNVRAGWOQZWPVLDVHLVMVRWQRDFJVEABWNCDGIHWNULZ
      VFVGVNWGWQWPRWIAGWBWOTUQVIVNAWCBWNEEEEWBWBWJWAWJWAWAWKWLWAVLWNWMLVMWNDEFJ
      KWRVJPVAVBVK $.
      $( [21-Jun-2010] $)
    $( In a topological group, a converse of a right translation is a
       homeomorphism.  Bourbaki TG III.2 (Contributed by FL, 30-Dec-2010.) $)
    cnvtrhom $p |- ( ( K e. TopGrp /\ A e. X )
        -> `' F e. ( J Homeo J ) ) $=
      ( ctopgrp wcel wa ctop chomeosm co ccnv topgrptop jca trhom cnvhmpha sylc
      adantr ) FLMZBGMZNEOMZUGNZCEEPQZMCRUIMUEUHUFUEUGUGEFKSZUJTUDABCDEFGHIJKUA
      CEEUBUC $.
      $( [30-Dec-2010] $)
    $( In a topological group the image of an open set by a right translation
       is an open set.  Bourbaki TG III.2 (Contributed by FL, 30-Dec-2010.) $)
    trhom2 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
       -> ( F " O ) e. J ) $=
      ( vy ctopgrp wcel cima chomeosm co wa wi trhom cv wral ctop topgrptop jca
      adantr hmeocnb syl wceq imaeq2 eleq1d rcla4cv syl6com mpcom 3impia ) FNOZ
      BHOZGEOZCGPZEOZCEEQROZUQURSZUSVATZABCDEFHIJKLUAVCVBCMUBZPZEOZMEUCZVDVCEUD
      OZVISZVBVHTUQVJURUQVIVIEFLUEZVKUFUGMCEEUHUIVGVAMGEVEGUJVFUTEVEGCUKULUMUNU
      OUP $.
      $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    tpgprop.5 $e |- .t = ( cset ` G ) $.
    $( In a topological group, the product of an open set by an element is
       open .  (Contributed by FL, 30-Dec-2010.) $)
    tpgprop1 $p |- ( ( K e. TopGrp /\ A e. X /\ O e. J )
        -> ( O .t { A } ) e. J ) $=
      ( ctopgrp wcel w3a csn cpw wceq co cima cgr topgrpgrp 3ad2ant1 simp2 cuni
      wi crn wss topgrpbs eqtr ex pwuni pweq syl5sseqr syl6 mpsyl a1d prsubrtr2
      sseld 3imp syl3anc trhom2 eqeltrd ) FOPZBHPZGEPZQZGBRIUAZCGUBZEVIDUCPZVGG
      HSZPZVJVKTVFVGVLVHDFLUDUEVFVGVHUFVFVGVHVNVFVHVNUHVGVFEVMGHDUIZTZVFVOEUGZT
      ZEVMUJZJDEFLMUKVPVRHVQTZVSVPVRVTHVOVQULUMVTVQSEVMEUNHVQUOUPUQURVAUSVBABGC
      DIHKJNUTVCABCDEFGHJKLMVDVE $.
      $( [1-Jan-2011] $) $( [30-Dec-2010] $)
    $( In a topological group, the product of an open set by another set is
       open .  (Contributed by FL, 1-Jan-2011.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    tpgprop2 $p |- ( ( K e. TopGrp /\ A e. ~P X /\ O e. J )
        -> ( O .t A ) e. J ) $=
      ( vy wcel cpw co cv 3ad2ant1 ctopgrp w3a csn ciun cgr wceq topgrpgrp cuni
      cdm wss pwuni topgrpbs syl5eq pweqd syl5sseqr ssel2 sylan 3adant2 syl5req
      crn grporndm syl eleqtrrd simp2 eqid iscst4 syl3anc ctop topgrptop simpl1
      wral wa elelpwi expcom 3ad2ant2 imp simpl3 cmpt tpgprop1 ralrimiva iunopn
      wi syl2anc eqeltrd ) FUAPZBHQZPZGEPZUBZGBIRZOBGOSZUCIRZUDZEWIDUEPZGDUIUIZ
      QZPBWPPWJWMUFWEWGWNWHDFLUGZTWIGWFWPWEWHGWFPZWGWEEWFUJWHWRWEEUHZQEWFEUKWEH
      WSWEHDUTZWSJDEFLMULUMUNUOEWFGUPUQURWIWOHWEWGWOHUFZWHWEWNXAWQWNHWTWOJDVAUS
      VBTUNZVCWIBWFWPWEWGWHVDXBVCOGBDIUEWOWOVENVFVGWIEVHPZWLEPZOBVKWMEPWEWGXCWH
      EFMVITWIXDOBWIWKBPZVLWEWKHPZWHXDWEWGWHXEVJWIXEXFWGWEXEXFWBWHXEWGXFWKBHVMV
      NVOVPWEWGWHXEVQAWKAHASWKDRVRZDEFGHIJXGVELMNVSVGVTOBWLEWAWCWD $.
      $( [15-Dec-2013] $) $( [1-Jan-2011] $)
  $}

  ${
    $d x A $.  $d x G $.  $d x J $.  $d x K $.  $d x X $.
    ltrhom.1 $e |- X = ran G $.
    ltrhom.2 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrhom.3 $e |- G = ( 1st ` K ) $.
    ltrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a left translation is a homeomorphism. ` A ` is
       constant.  (Contributed by FL, 12-Jul-2012.)  (The proof was shortened
       by Mario Carneiro, 31-May-2014.) $)
    ltrhom $p |- ( ( K e. TopGrp /\ A e. X ) -> F e. ( J Homeo J ) ) $=
      ( wcel wa co ccn adantr cmpt wceq eqid cfv ctopgrp ccnv ctop wb topgrptop
      chomeosm hmeobc syl2anc cuni crn topgrpbs syl5eq mpteq1 syl simpr eleqtrd
      cv cnmptc cnmptid ctx topgrpcn cnmpt12f eqeltrd topgrpgrp ltrinvlem sylan
      cgn cgr cgs eqtrd grpoinvcl mpbir2and ) FUALZBGLZMZCEEUFNLZCEEONZLZCUBZVQ
      LZVOEUCLZWAVPVRVTMUDVMWAVNEFKUEPZWBCEEUGUHVOCAEUIZBAUQZDNZQZVQVOCAGWEQZWF
      IVOGWCRZWGWFRVOGDUJZWCHVMWIWCRVNDEFJKUKPULZAGWCWEUMUNULVOABWDDEEEEWCWCWCS
      ZWBWKWBWBVOABEEWCWCWKWBWKWBVOBGWCVMVNUOWJUPURVOAEWCWKWBUSZWBVMDEEUTNEONLV
      NDEFJKVAPZVBVCVOVSAWCBDVGTZTZWDDNZQZVQVOVSAGWPQZWQVMDVHLZVNVSWRRDFJVDZABD
      VITZCDWNGIHXASWNSZVEVFVOWHWRWQRWJAGWCWPUMUNVJVOAWOWDDEEEEWCWCWKWBWKWBWBVO
      AWOEEWCWCWKWBWKWBVOWOGWCVMWSVNWOGLWTBDWNGHXBVKVFWJUPURWLWBWMVBVCVL $.
      $( [31-May-2014] $) $( [12-Jul-2012] $)
  $}


  ${
    $d x y A $.  $d x y B $.  $d x y G $.  $d x y J $.  $d x y K $.
    $d x y X $.
    lrtrhom.1 $e |- X = ran G $.
    lrtrhom.2 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    lrtrhom.3 $e |- G = ( 1st ` K ) $.
    lrtrhom.4 $e |- J = ( 2nd ` K ) $.
    $( In a topological group, a right and left translation is a
       homeomorphism.  Note that ` A ` and ` B ` are constant.  (Contributed by
       FL, 12-Jul-2012.) $)
    lrtrhom $p |- ( ( K e. TopGrp /\ A e. X /\ B e. X )
       -> F e. ( J Homeo J ) ) $=
      ( vy ctopgrp wcel w3a cv co cmpt eqid ccom chomeosm cgr topgrpgrp syl3an1
      wceq cmperltr ctop wa topgrptop 3ad2ant1 trhom 3adant2 ltrhom 3adant3 jca
      3jca cmphmp sylc eqeltrd ) GNOZBHOZCHOZPZDMHBMQERSZAHAQCERSZUAZFFUBRZVAEU
      COVBVCDVGUFEGKUDAMBCVEDEVFHJVETZVFTZIUGUEVDFUHOZVKVKPZVFVHOZVEVHOZUIVGVHO
      VAVBVLVCVAVKVKVKFGLUJZVOVOUQUKVDVMVNVAVCVMVBACVFEFGHIVJKLULUMVAVBVNVCMBVE
      EFGHIVIKLUNUOUPVFVEFFFURUSUT $.
      $( [12-Jul-2012] $)
  $}

  ${
    $d A o u v x $.  $d B o u v x $.  $d G a b o u v $.  $d X a b o u v x $.
    grphmlem0.1 $e |- H = { o | E. a e. X E. b e. X
         o = ( x e. X |-> ( ( a G x ) G b ) ) } $.
    $( Condition for a left and right translation to belong to the class of all
       the left and right translations over the class ` X ` .  (Contributed by
       FL, 16-Jul-2012.) $)
    grphmlem0 $p |- ( ( A e. X /\ B e. X /\ X e. C )
       -> ( x e. X |-> ( ( A G x ) G B ) ) e. H ) $=
      ( vu vv wcel cv co cmpt wceq wrex mpteq2dv eqid oveq1 oveq1d eqeq2d oveq2
      w3a cab rcla42ev mp3an3 3adant3 cvv mptexg 3ad2ant3 eqeq1 adantr rexbidva
      wb elabg syl mpbird rexbidv cbvrexv syl6bb abbii syl6eleqr ) BHNZCHNZHDNZ
      UFZAHBAOZFPZCFPZQZEOZAHIOZVJFPZJOZFPZQZRZJHSZIHSZEUGZGVIVMVNAHLOZVJFPZMOZ
      FPZQZRZMHSZLHSZEUGZWCVIVMWLNZVMWHRZMHSZLHSZVFVGWPVHVFVGVMVMRZWPVMUAWNWQVM
      AHVKWFFPZQZRLMBCHHWDBRZWHWSVMWTAHWGWRWTWEVKWFFWDBVJFUBUCTUDWFCRZWSVMVMXAA
      HWRVLWFCVKFUETUDUHUIUJVIVMUKNZWMWPUQVHVFXBVGAHVLDULUMWKWPEVMUKVNVMRZWJWOL
      HXCWJWOUQWDHNXCWIWNMHXCWIWNUQWFHNVNVMWHUNUOUPUOUPURUSUTWBWKEWAWJILHVOWDRZ
      WAVNAHWEVQFPZQZRZJHSWJXDVTXGJHXDVSXFVNXDAHVRXEXDVPWEVQFVOWDVJFUBUCTUDVAXG
      WIJMHVQWFRZXFWHVNXHAHXEWGVQWFWEFUETUDVBVCVBVDVEKVE $.
      $( [23-Aug-2014] $) $( [16-Jul-2012] $)
  $}

  ${
    grphm.1 $e |- X = ran G $.
    grphm.2 $e |- G = ( 1st ` K ) $.
    grphm.3 $e |- J = ( 2nd ` K ) $.
    grphm.4 $e |- H = { o | E. a e. X E. b e. X
         o = ( p e. X |-> ( ( a G p ) G b ) ) } $.

    ${
      $d G a b c d e f o p $.  $d G c d e f p q $.  $d G c d p q z $.
      $d H c d e f y $.  $d K c d e f x y $.  $d X a b c d e f o p $.
      $d X c d e f p q $.  $d o x y $.
      $( Lemma for ~ grpohm .  Closure. $)
      grpohmlem1 $p |- ( K e. TopGrp
       -> A. x e. H
             A. y e. H ( x o. y ) e. H ) $=
        ( wcel cv co wceq wrex vc vd ve vf vq vz ctopgrp ccom wa cmpt wi eleq2i
        cab anbi12i oveq1 oveq1d mpteq2dv eqeq2d oveq2 cbvrex2v abbii bitri vex
        ax-17 simpl eqeq1d 2rexbidva elabf eqeq1 adantr cbvmpt eqeq2i topgrpgrp
        wb w3a cgr 3ad2ant3 anim12i ancoms 3adant3 simpr cmprltr syl3anc simp2l
        eqid simp1l grpocl simp1r simp2r rcla42ev cvv crn c1st cfv fvex eqeltri
        eqidd rnex mptexg ax-mp a1i elab3 sylibr syl6eleqr eqeltrd 3exp coeq12d
        eleq1d imbi2d syl5ibr sylbi com4l com12 rexlimivv imp syl2anb ralrimivv
        ex com3l ) GUGPZAQZBQZUHZEPZABEEYAEPZYBEPZUIZXTYDYGYACQZIHUAQZIQZDRZUBQ
        ZDRZUJZSZUBHTUAHTZCUMZPZYBYHIHUCQZYJDRZUDQZDRZUJZSZUDHTUCHTZCUMZPZUIZXT
        YDUKZYGYAYHIHJQZYJDRZKQZDRZUJZSZKHTJHTZCUMZPZYBUUQPZUIUUHYEUURYFUUSEUUQ
        YAOULEUUQYBOULUNUURYRUUSUUGUUQYQYAUUPYPCUUOYOYHIHYKUULDRZUJZSJKUAUBHHUU
        JYISZUUNUVAYHUVBIHUUMUUTUVBUUKYKUULDUUJYIYJDUOUPUQURUULYLSZUVAYNYHUVCIH
        UUTYMUULYLYKDUSUQURUTVAULUUQUUFYBUUPUUECUUOUUDYHIHYTUULDRZUJZSJKUCUDHHU
        UJYSSZUUNUVEYHUVFIHUUMUVDUVFUUKYTUULDUUJYSYJDUOUPUQURUULUUASZUVEUUCYHUV
        GIHUVDUUBUULUUAYTDUSUQURUTVAULUNVBYRYAYNSZUBHTUAHTZYBUUCSZUDHTUCHTZUUIU
        UGYPUVICYAUVICVDAVCYHYASZYOUVHUAUBHHUVLYIHPZYLHPZUIZUIYHYAYNUVLUVOVEVFV
        GVHUUEUVKCYBUVKCVDBVCYHYBSZUUDUVJUCUDHHUVPUUDUVJVNYSHPZUUAHPZUIZYHYBUUC
        VIVJVGVHUVIUVKUUIUVHUVKUUIUKUAUBHHUVKUVOUVHUUIUVJUVOUVHUUIUKUKZUCUDHHUV
        JUVSUVTUVHUVJUVSUVOUUIUVHYAUEHYIUEQZDRZYLDRZUJZSZUVJUVSUVOUUIUKZUKZUKYN
        UWDYAIUEUFHYMUWCUFQZYMPUEVDUWHUWCPIVDYJUWASYKUWBYLDYJUWAYIDUSUPVKVLUWEU
        VJUWGUVSUWFUWEUVJUIZUVOXTUWDUUCUHZEPZUKZUKUVSUVOXTUWKUVSUVOXTVOZUWJIHYI
        YSDRZYJDRZUUAYLDRZDRZUJZEUWMDVPPZUVMUVQUIZUVNUVRUIZUWJUWRSXTUVSUWSUVODG
        MVMVQZUVSUVOUWTXTUVOUVSUWTUVOUVMUVSUVQUVMUVNVEUVQUVRVEVRVSVTUVSUVOUXAXT
        UVOUVSUXAUVOUVNUVSUVRUVMUVNWAUVQUVRWAVRVSVTIUEYIYSYLUUAUUCUWDDHUWDWEUUC
        WELWBWCUWMUWRUUQEUWMUWRUUNSZKHTJHTZUWRUUQPUWMUWNHPZUWPHPZUWRUWRSZUXDUWM
        UWSUVMUVQUXEUXBUVSUVMUVNXTWDUVQUVRUVOXTWFYIYSDHLWGWCUWMUWSUVRUVNUXFUXBU
        VQUVRUVOXTWHUVSUVMUVNXTWIUUAYLDHLWGWCUWMUWRWQUXCUXGUWRIHUWOUULDRZUJZSJK
        UWNUWPHHUUJUWNSZUUNUXIUWRUXJIHUUMUXHUXJUUKUWOUULDUUJUWNYJDUOUPUQURUULUW
        PSZUXIUWRUWRUXKIHUXHUWQUULUWPUWODUSUQURWJWCUUPUXDCUWRUWRWKPZUXDHWKPUXLH
        DWLWKLDDGWMWNWKMGWMWOWPWRWPIHUWQWKWSWTXAYHUWRSZUUOUXCJKHHUXMUUJHPUULHPU
        IZUIYHUWRUUNUXMUXNVEVFVGXBXCOXDXEXFUWIUUIUWLUVOUWIYDUWKXTUWIYCUWJEUWIYA
        UWDYBUUCUWEUVJVEUWEUVJWAXGXHXIXIXJXRXKXLXMXNXSXNXOXPXKXMXQ $.
        $( [23-Aug-2014] $) $( [26-Jun-2012] $)
    $}

    ${
      $d G a b c d e i p o $.  $d H g h x y z $.  $d K a b c d e f g h i p $.
      $d X a b c d e i p o $.  $d f g h o x y z $.
      $( Lemma for ~ grpohm .  Associativity . $)
      grpohmlem2 $p |- ( K e. TopGrp -> A. f e. H A. g e. H A. h e. H (
      ( f { <. <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x
      o. y ) ) } g ) { <. <. x , y >. , z >. | ( x e. H /\ y e. H
      /\ z = ( x o. y ) ) } h ) = ( f { <. <. x , y >. , z >. | ( x
      e. H /\ y e. H /\ z = ( x o. y ) ) } ( g { <. <. x , y >. , z
      >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) } h ) ) ) $=
        ( wceq vc vd ve vi ctopgrp wcel cv ccom w3a copab2 co wral wa cmpt wrex
        cab vex simpl eqeq1d 2rexbidva elab eleq2i eqeq1 rexbidv oveq1 mpteq2dv
        wi oveq1d eqeq2d oveq2 cbvrex2v cgr topgrpgrp adantl anim12i simpr 3jca
        adantr eqid cmprltr2 syl coeq1d ad2antlr simplll simplrl grpocl syl3anc
        simplrr simpllr simprr syl112anc simprl syl122anc coeq2d grpoass eqcomd
        syl2anc jca oveq12d 3eqtr4rd 3eqtrd coeq2 coeq1 eqeq12d syl5ibr expdimp
        imp exp3a exp4c ex com14 3imp impcom com12 rexlimivv 3exp com3l syl2anb
        sylbi eleq2s ralrimiva ralrimivva ax-17 cvv grpohmlem1 ra42 coex df-3an
        oprabbii ov2g imp31 ralbida 2ralbida mpbird coexg oveqi syl5eq oveq2d
        a1i ) KUEUFZDUGZEUGZAUGZIUFZBUGZIUFZCUGUUCUUEUHZTZUIZABCUJZUKZFUGZUUJUK
        ZUUAUUBUULUUJUKZUUJUKZTZFIULZEIULDIULUUAUUBUHZUULUUJUKZUUAUUBUULUHZUUJU
        KZTZFIULZEIULDIULZYTUVDUURUULUHZUUAUUTUHZTZFIULZEIULDIULYTUVHDEIIYTUUAI
        UFZUUBIUFZUMZUMZUVGFIUULIUFZUVLUVGUVLUVGVGZUULGUGZMLNUGZMUGZHUKZOUGZHUK
        ZUNZTZOLUOZNLUOZGUPZIUULUWEUFUULUWATZOLUONLUOZUVNUWDUWGGUULFUQZUVOUULTZ
        UWBUWFNOLLUWIUVPLUFZUVSLUFZUMZUMUVOUULUWAUWIUWLURUSUTVAUVLUWGUVGUVKYTUW
        GUVGVGZUVIUUAUWEUFZUUBUWEUFZYTUWMVGZUVJIUWEUUASVBIUWEUUBSVBUWNUUAUWATZO
        LUOZNLUOZUUBUWATZOLUOZNLUOZUWPUWOUWDUWSGUUADUQZUVOUUATZUWCUWRNLUXDUWBUW
        QOLUVOUUAUWAVCVDVDVAUWDUXBGUUBEUQZUVOUUBTZUWCUXANLUXFUWBUWTOLUVOUUBUWAV
        CVDVDVAUWSUUAMLUAUGZUVQHUKZUBUGZHUKZUNZTZUBLUOUALUOZUUBMLUCUGZUVQHUKZUD
        UGZHUKZUNZTZUDLUOUCLUOZUWPUXBUWQUXLUUAMLUXHUVSHUKZUNZTNOUAUBLLUVPUXGTZU
        WAUYBUUAUYCMLUVTUYAUYCUVRUXHUVSHUVPUXGUVQHVEVHVFVIUVSUXITZUYBUXKUUAUYDM
        LUYAUXJUVSUXIUXHHVJVFVIVKUWTUXSUUBMLUXOUVSHUKZUNZTNOUCUDLLUVPUXNTZUWAUY
        FUUBUYGMLUVTUYEUYGUVRUXOUVSHUVPUXNUVQHVEVHVFVIUVSUXPTZUYFUXRUUBUYHMLUYE
        UXQUVSUXPUXOHVJVFVIVKUXMUXTUWPUXLUXTUWPVGUAUBLLUXTUXGLUFZUXILUFZUMZUXLU
        WPUXSUYKUXLUWPVGZVGUCUDLLUXNLUFZUXPLUFZUMZUXSUYKUYLUYOUXSUYKUIZUXLYTUWM
        UWGUYPUXLYTUIZUVGUWFUYQUVGVGZNOLLUWFUWLUYRUWFUWLUYQUVGUWLUYQUMUVGUWFUUR
        UWAUHZUUAUUBUWAUHZUHZTZUYQUWLVUBUYPUXLYTUWLVUBVGZUYOUXSUYKUXLYTVUCVGZVG
        UXLUXSUYKUYOVUDUXLUXSUYKUYOVUDVGVGUXLUXSUMZUYKUYOYTVUCVUEUYKUYOUMZYTUMZ
        UWLVUBUXLUXSVUGUWLUMZVUBUXSVUHUMVUBUXLUXKUUBUHZUWAUHZUXKUYTUHZTZUXSVUHV
        ULVUHVULUXSUXKUXRUHZUWAUHZUXKUXRUWAUHZUHZTVUHVUNMLUXGUXNHUKZUVQHUKUXPUX
        IHUKZHUKUNZUWAUHZMLVUQUVPHUKZUVQHUKZUVSVURHUKZHUKZUNZVUPVUHVUMVUSUWAVUH
        HVLUFZUYIUYMUMZUYJUYNUMZUIZVUMVUSTVUGVVIUWLVUGVVFVVGVVHYTVVFVUFHKQVMZVN
        ZVUFVVGYTUYKUYIUYOUYMUYIUYJURUYMUYNURVOVRVUFVVHYTUYKUYJUYOUYNUYIUYJVPUY
        MUYNVPVOVRVQVRMMUXGUXNUXIUXPUXRUXKHLUXKVSZUXRVSZPVTWAWBVUHVVFVUQLUFZUWJ
        UMVURLUFZUWKVUTVVETYTVVFVUFUWLVVJWCZVUGVVNUWLUWJVUGVVFUYIUYMVVNVVKUYIUY
        JUYOYTWDZUYKUYMUYNYTWEZUXGUXNHLPWFWGUWJUWKURVOVUHVVFUYNUYJVVOVVPVUGUYNU
        WLUYKUYMUYNYTWHVRZVUGUYJUWLUYIUYJUYOYTWIVRZUXPUXIHLPWFWGVUGUWJUWKWJZMMV
        UQUVPVURUVSUWAVUSHLVUSVSUWAVSZPVTWKVUHUXKMLUXNUVPHUKZUVQHUKUVSUXPHUKZHU
        KUNZUHZMLUXGVWCHUKZUVQHUKZVWDUXIHUKZHUKZUNZVUPVVEVUHVVFUYIVWCLUFZUYJVWD
        LUFZVWFVWKTVVPVUGUYIUWLVVQVRZVUHVVFUYMUWJVWLVVPVUGUYMUWLVVRVRZVUGUWJUWK
        WLZUXNUVPHLPWFWGVVTVUHVVFUWKUYNVWMVVPVWAVVSUVSUXPHLPWFWGMMUXGVWCUXIVWDV
        WEUXKHLVVLVWEVSPVTWMVUHVUOVWEUXKVUHVVFUYMUWJUYNUWKVUOVWETVVPVWOVWPVVSVW
        AMMUXNUVPUXPUVSUWAUXRHLVVMVWBPVTWMWNVUHMLVVDVWJVUHVVBVWHVVCVWIHVUHVVAVW
        GUVQHVUHVVFUYIUYMUWJUIVVAVWGTVVPVUHUYIUYMUWJVWNVWOVWPVQUXGUXNUVPHLPWOWQ
        VHVUHVVFUWKUYNUYJUIZUMZVVCVWITVUHVVFVWQVVPVUHUWKUYNUYJVWAVVSVVTVQWRVWRV
        WIVVCUVSUXPUXIHLPWOWPWAWSVFWTXAUXSVUJVUNVUKVUPUXSVUIVUMUWAUUBUXRUXKXBWB
        UXSUYTVUOUXKUUBUXRUWAXCWNXDXEXGUXLUYSVUJVUAVUKUXLUURVUIUWAUUAUXKUUBXCWB
        UUAUXKUYTXCXDXEXFXHXIXJXKXLXLXMUWFUVEUYSUVFVUAUULUWAUURXBUWFUUTUYTUUAUU
        LUWAUUBXBWNXDXEXHXNXOXNXPXPXOXQXOXGXRXRXRXMXNXSSXTXMYAYBYTUVCUVHDEIIYTD
        YCZYTEYCZUVLUVBUVGFIUVLFYCZUVLUVMUMZUUSUVEUVAUVFVXBUURIUFZUVMUVEYDUFZUU
        SUVETUVLVXCUVMYTUVKVXCYTVXCEIULDIULUVKVXCVGDEGHIJKLMNOPQRSYEVXCDEIIYFWA
        XGVRUVLUVMVPZVXDVXBUURUULUUAUUBUXCUXEYGUWHYGYSABCUURUULIIUUGUVEUUJUURUU
        EUHYDUUCUURUUEXCUUEUULUURXBUUIUUDUUFUMUUHUMZABCUUDUUFUUHYHYIZYJWGVXBUVI
        UUTIUFZUVFYDUFZUVAUVFTYTUVIUVJUVMWEZYTUVKUVMVXHUVKYTUVMVXHVGZUVJYTVXKVG
        UVIYTUVJVXKYTUVJUVMVXHYTVXHFIULEIULUVJUVMUMVXHVGEFGHIJKLMNOPQRSYEVXHEFI
        IYFWAXHXNVNXNYKVXIVXBUUAUUTUXCUUBUULUXEUWHYGZYGYSABCUUAUUTIIUUGUVFUUJUU
        AUUEUHZYDUUCUUAUUEXCZUUEUUTUUAXBVXGYJWGXDYLYMYNYTUUQUVCDEIIVWSVWTUVLUUP
        UVBFIVXAVXBUUMUUSUUOUVAVXBUUKUURUULUUJVXBUVIUVJUURYDUFZUUKUURTVXJYTUVIU
        VJUVMWHZUVKVXOYTUVMUUAUUBIIYOWCABCUUAUUBIIUUGUURUUJVXMYDVXNUUEUUBUUAXBV
        XGYJWGVHVXBUUNUUTUUAUUJVXBUVJUVMUUTYDUFZUUNUUTTVXPVXEVXQVXBVXLYSUVJUVMV
        XQUIUUNUUBUULVXFABCUJZUKUUTUUJVXRUUBUULVXGYPABCUUBUULIIUUGUUTVXRUUBUUEU
        HYDUUCUUBUUEXCUUEUULUUBXBVXRVSYJYQWGYRXDYLYMYN $.
        $( [23-Aug-2014] $) $( [29-Jun-2012] $)
    $}

    ${
      $d G a b f o p u v x y z $.  $d H f x y z $.  $d K a b f p $.
      $d X a b f o p u x y z $.

      $( Lemma for ~ grpohm .  Neutral element . $)
      grpohmlem3 $p |- ( ( K e. TopGrp /\ ( x e. X |-> ( ( ( Id ` G ) G
                         x ) G ( Id ` G ) ) ) e. H ) -> A. f e. H ( (
                         x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G )
                         ) ) { <. <. x , y >. , z >. | ( x e. H /\
                         y e. H /\ z = ( x o. y ) ) } f ) = f ) $=
        ( co wcel wceq vu vv cgi cfv cv cmpt ctopgrp ccom w3a copab2 wral ax-17
        oveq2 oveq1d cbvmpt eleq1i wa wi oveq1i cvv wrex cab cgr topgrpgrp eqid
        grpoidcl syl adantr a1i oveq1 mpteq2dv eqeq2d rcla42ev syl3anc crn c1st
        fvex eqeltri rnex mptexg ax-mp eqeq1 rexbidv sylibr syl6eleqr simpr vex
        elab coex coeq1 df-3an oprabbii adantl simpll simplr cmprltr2 syl122anc
        coeq2 ov2g eqidd grpolid impcom adantlr grporid com12 oveq12d mpteq12dv
        ex imp eqtrd id eqeq12d imbi2d syl5ibrcom rexlimivv sylbi eleq2s syl5eq
        ralrimiv sylan2b ) AJFUCUDZAUEZFRZYAFRZUFZGSIUGSZUAJYAUAUEZFRZYAFRZUFZG
        SZYEDUEZYBGSZBUEZGSZCUEYBYNUHZTZUIZABCUJZRZYLTZDGUKYEYJGAUAUBJYDYIUBUEZ
        YDSUAULUUBYISZAULYBYGTYCYHYAFYBYGYAFUMUNUOZUPYFYKUQUUADGYFYLGSZUUAURYKY
        FUUEUUAYFUUEUQZYTYJYLYSRZYLYEYJYLYSUUDUSUUFUUGYJYLUHZYLUUFYKUUEUUHUTSZU
        UGUUHTUUFYJEUEZKJLUEZKUEZFRZMUEZFRZUFZTZMJVAZLJVAZEVBZGUUFYJUUPTZMJVAZL
        JVAZYJUUTSUUFYAJSZUVDYJKJYAUULFRZYAFRZUFZTZUVCYFUVDUUEYFFVCSZUVDFIOVDZY
        AFJNYAVEZVFVGZVHZUVMUVHUUFUAKUBJYIUVFUUCKULUUBUVFSUAULYGUULTYHUVEYAFYGU
        ULYAFUMUNUOVIUVAUVHYJKJUVEUUNFRZUFZTLMYAYAJJUUKYATZUUPUVOYJUVPKJUUOUVNU
        VPUUMUVEUUNFUUKYAUULFVJUNVKVLUUNYATZUVOUVGYJUVQKJUVNUVFUUNYAUVEFUMVKVLV
        MVNUUSUVCEYJJUTSYJUTSJFVOUTNFFIVPUDUTOIVPVQVRVSVRUAJYIUTVTWAZUUJYJTZUUR
        UVBLJUVSUUQUVAMJUUJYJUUPWBWCWCWHWDQWEYFUUEWFUUIUUFYJYLUVRDWGZWIVIABCYJY
        LGGYPUUHYSYJYNUHUTYBYJYNWJYNYLYJWRYRYMYOUQYQUQABCYMYOYQWKWLWSVNUUEYFUUH
        YLTZYFUWAURZYLUUTGYLUUTSYLUUPTZMJVAZLJVAZUWBUUSUWEEYLUVTUUJYLTZUURUWDLJ
        UWFUUQUWCMJUUJYLUUPWBWCWCWHUWCUWBLMJJUUKJSZUUNJSZUQZUWBUWCYFYJUUPUHZUUP
        TZURUWIYFUWKUWIYFUQZUWJKJYAUUKFRZUULFRZUUNYAFRZFRZUFZUUPUWLUVIUVDUWGUVD
        UWHUWJUWQTYFUVIUWIUVJWMYFUVDUWIUVLWMZUWGUWHYFWNUWRUWGUWHYFWOKUAYAUUKYAU
        UNUUPYJFJYJVEUUPVENWPWQUWLKJUWPJUUOUWLJWTUWLUWNUUMUWOUUNFUWGYFUWNUUMTUW
        HUWGYFUQUWMUUKUULFYFUWGUWMUUKTZYFUVIUWGUWSURUVJUVIUWGUWSUUKYAFJNUVKXAXH
        VGXBUNXCUWIYFUWOUUNTZUWHYFUWTURUWGYFUWHUWTYFUVIUWHUWTURUVJUVIUWHUWTUUNY
        AFJNUVKXDXHVGXEWMXIXFXGXJXHUWCUWAUWKYFUWCUUHUWJYLUUPYLUUPYJWRUWCXKXLXMX
        NXOXPQXQXBXJXRXHVHXSXT $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d o p G $.  $d h H $.  $d a b h o p J $.  $d a b h p K $.  $d b o p X $.
      $( Lemma for ~ grpohm .  Homeomorphisms . $)
      grpohmlem4 $p |- ( K e. TopGrp -> H C_ ( J Homeo J ) ) $=
        ( wcel cv cima wral wi wceq wrex vh ctopgrp chomeosm cuni wf1o ccnv w3a
        co cmpt cab vex eqeq1 2rexbidv elab wa crn topgrpbs eqtr topgrpgrp eqid
        cgr rltrooo 3exp syl wb f1oeq23 anidms imbi2d syl5ibr eqcoms mpan mpcom
        com3l 3imp ctop topgrptop 3ad2ant3 lrtrhom 3coml hmeocnb hmeocna 3expia
        jca sylc 3jca f1oeq1 imaeq1 eleq1d ralbidv imaeq1d 3anbi123d syl5ibrcom
        cnveq rexlimivv sylbi eleq2s com12 ishomeo syl2anc sylibrd ssrdv ) EUBN
        ZUACDDUCUHZXBUAOZCNZDUDZXFXDUEZXDAOZPZDNZADQZXDUFZXHPZDNZADQZUGZXDXCNZX
        EXBXPXBXPRZXDXHGFHOZGOBUHIOZBUHUIZSZIFTHFTZAUJZCXDYDNXDYASZIFTHFTZXRYCY
        FAXDUAUKXHXDSYBYEHIFFXHXDYAULUMUNYEXRHIFFXSFNZXTFNZUOXRYEXBXFXFYAUEZYAX
        HPZDNZADQZYAUFZXHPZDNZADQZUGZRYGYHXBYQYGYHXBUGZYIYLYPYGYHXBYIXBYGYHYIBU
        PZXFSZXBYGYHYIRZRZBDEKLUQFYSSZYTXBUUBRZJUUCYTUOFXFSUUDFYSXFURUUDXFFXBUU
        BXFFSZYGYHFFYAUEZRZRZXBBVANZUUHBEKUSUUIYGYHUUFGXSXTYABFYAUTZJVBVCVDUUEU
        UAUUGYGUUEYIUUFYHUUEYIUUFVEXFFXFFYAVFVGVHVHVIVJVDVKVLVMVNYRDVONZUUKUOZY
        AXCNZYLXBYGUULYHXBUUKUUKDELVPZUUNWCVQXBYGYHUUMGXSXTYABDEFJUUJKLVRVSZAYA
        DDVTWDYRUULUUMYPYRUUKUUKXBYGUUKYHUUNVQZUUPWCUUOAYADDWAWDWEWBYEXPYQXBYEX
        GYIXKYLXOYPXFXFXDYAWFYEXJYKADYEXIYJDXDYAXHWGWHWIYEXNYOADYEXMYNDYEXLYMXH
        XDYAWMWJWHWIWKVHWLWNWOMWPWQXBUUKUUKXQXPVEUUNUUNAXDDDXFXFXFUTZUUQWRWSWTX
        A $.
        $( [12-Jul-2012] $)
    $}

    ${
      $d G a b g p u x y z $.  $d G a b o p u $.  $d G b g p u v x y z $.
      $d G p u v w x $.  $d H g u v x y z $.  $d K f u v $.  $d K u v x $.
      $d X a b g p u x y z $.  $d X a b o p u $.  $d X b g p u v x y z $.
      $d a b f g u $.  $d f o u v $.
      $( Lemma for ~ grpohm .  Inverse element . $)
      grpohmlem5 $p |- ( K e. TopGrp -> A. f e. H E. g e. H ( g { <.
      <. x , y >. , z >. | ( x e. H /\ y e. H /\ z = ( x o. y ) ) }
      f ) = ( x e. X |-> ( ( ( Id ` G ) G x ) G ( Id ` G ) ) ) ) $=
        ( wcel co vu vv vw ctopgrp cv ccom wceq w3a copab2 cgi cfv cmpt wrex wi
        cab vex wb wa simpl eqeq1d rexbidva adantr oveq1 oveq1d mpteq2dv eqeq2d
        elab oveq2 cbvrex2v cgn cvv cgr topgrpgrp adantl eqid grpoinvcl syl2anc
        simpll simplr crn rnexg syl syl5eqel grphmlem0 ov2gc cmprltr2 syl122anc
        syl3anc ax-17 cbvmpt oveq12d syl5eq 3eqtrd rcla4ev expcom com12 rexbidv
        grpolinv grporinv syl5ibr com3l mpcom ex com23 rexlimivv sylbi ralrimiv
        imbi2d eleq2s ) JUDSZEUEZDUEZAUEZHSBUEZHSCUEXMXNUFUGUHABCUIZTZAKGUJUKZX
        MGTZXQGTZULZUGZEHUMZDHXLHSXJYBXJYBUNZXLFUEZLKMUEZLUEZGTZNUEZGTZULZUGZNK
        UMZMKUMZFUOZHXLYNSXLYJUGZNKUMZMKUMZYCYMYQFXLDUPYDXLUGZYLYPMKYRYLYPUQYEK
        SYRYKYONKYRYHKSZURYDXLYJYRYSUSUTVAVBVAVGYQXLLKUAUEZYFGTZUBUEZGTZULZUGZU
        BKUMUAKUMYCYOUUEXLLKUUAYHGTZULZUGMNUAUBKKYEYTUGZYJUUGXLUUHLKYIUUFUUHYGU
        UAYHGYEYTYFGVCVDVEVFYHUUBUGZUUGUUDXLUUILKUUFUUCYHUUBUUAGVHVEVFVIUUEYCUA
        UBKKYTKSZUUBKSZURZXJUUEYBUULXJUUEYBUNZLKYTGVJUKZUKZYFGTUUBUUNUKZGTULZHS
        ZUULXJURZUUMUUSUUOKSZUUPKSZKVKSZUURUUSGVLSZUUJUUTXJUVCUULGJPVMVNZUUJUUK
        XJVRZYTGUUNKOUUNVOZVPVQZUUSUVCUUKUVAUVDUUJUUKXJVSZUUBGUUNKOUVFVPVQZUUSK
        GVTZVKOUUSUVCUVJVKSUVDGVLWAWBWCZLUUOUUPVKFGHKMNRWDWHZUUEUURUUSYBUURUUSY
        BUNUUEUUSXKUUDXOTZXTUGZEHUMZUNUUSUURUVOUUSUUQUUDXOTZXTUGZUURUVOUNUUSUVP
        UUQUUDUFZLKUUOYTGTZYFGTZUUBUUPGTZGTZULZXTUUSUURUUDHSZUVPUVRUGUVLUUSUUJU
        UKUVBUWDUVEUVHUVKLYTUUBVKFGHKMNRWDWHABCUUQUUDHHXOXOVOWEVQUUSUVCUUTUUJUV
        AUUKUVRUWCUGUVDUVGUVEUVIUVHLLUUOYTUUPUUBUUDUUQGKUUQVOUUDVOOWFWGUUSUWCAK
        UVSXMGTZUWAGTZULXTLAUCKUWBUWFUCUEZUWBSAWIUWGUWFSLWIYFXMUGUVTUWEUWAGYFXM
        UVSGVHVDWJUUSAKUWFXSUUSUWEXRUWAXQGUUSUVSXQXMGUUSUVCUUJUVSXQUGUVDUVEYTXQ
        GUUNKOXQVOZUVFWRVQVDUUSUVCUUKUWAXQUGUVDUVHUUBXQGUUNKOUWHUVFWSVQWKVEWLWM
        UURUVQUVOUVNUVQEUUQHXKUUQUGUVMUVPXTXKUUQUUDXOVCUTWNWOWBWPUUEYBUVOUUSUUE
        YAUVNEHUUEXPUVMXTXLUUDXKXOVHUTWQXHWTXAXBXCXDXEXFXFRXIWPXG $.
        $( [23-Aug-2014] $) $( [16-Jul-2012] $)
    $}

    ${
      $d a b f g h o u x y z H $.  $d a a0 a1 b b0 b1 f g o p q u v x y z X $.
      $d a a0 a1 b b0 b1 f g o p q u v w x y z G $.  $d a b o p J $.
      $d a a0 a1 b b0 b1 f g h o p u x y K $.
      $( The functions of equation ` ( ( a G p ) G b ) ` form a group of
         homeomorphisms.  (Contributed by FL, 17-Jul-2012.) $)
      grpohm $p |- ( K e. TopGrp -> ( { <. <. x , y >. , z >. |
         ( x e. H /\ y e. H /\ z = ( x o. y ) ) } e. GrpOp /\
         H C_ ( J Homeo J ) ) ) $=
        ( wcel wceq co wa vf vg vh vu vv bnjva1 bnjvb1 bnjva0 bnjvb0 vq ctopgrp
        cv ccom w3a copab2 cgr chomeosm wss crn cxp wral wrex cmpt cab wex eqid
        wf wi oveq1 oveq1d mpteq2dv eqeq2d rcla42ev mp3an3 adantr cvv topgrpgrp
        oveq2 wb unsgrp syl adantl mptexg eqeq1 2rexbidv elabg mpbird syl6eleqr
        cbvrex2v abbii 3adant2 c1st cfv cgi grpoidcl eqcomi eleq1i simpl eqcomd
        fveq2 eleq1d syl5ibr ax-mp simpr grpocl syl3anc jca sylanb fveq2i eqidd
        mpteq12dv 3exp com3r eleq2s mpcom 3ad2ant3 rcla4ev syl2anc df-3an sylib
        rexbidv rexlimivv com12 vex elab ax-17 anbi12d ex cbvexd df-rex 3bitr4g
        a17d cbvrexv anim12i cbvmptv 3imp sylbi a1i eqeltri raleqbi1dv a1d syl6
        grporid grpolid eqtr2d com4t pm2.43i rneqi eqtr4i impcom rnexg syl5eqel
        mpteq2dva simp2 ad2antlr simplll simpllr fmptd crimmt1 coeq2 coeq1 r2ex
        eqtr4d bicomi 2exbii oveq12d eleq2i id oveqan12d 3bitri coeq12d simp2ll
        bitri simp2rl simp2rr cmprltr2 biimpd syl6bi com4r com23 com3l exlimdvv
        simp2lr impbid abbidv 3anbi3d 2exbidv cbvabv rnoprab 3eqtr4g grpohmlem1
        syl6eq oprabbii grpohmlem2 fvex rnex mptex sylibr grpohmlem3 grpohmlem5
        foprab2 r19.26 sylanbrc eqeq1d eqeq2 ralbidv rcla4edv mpdan 3jca xpeq12
        mpd anidms feq23 mpancom raleq rexeq anbi2d rexeqbi1dv 3anbi123d eqcoms
        ab2rexex pm3.2i oprabex2g isgrpo grpohmlem4 ) HUKQZAULZFQZBULZFQZCULZUY
        GUYIUMZRZUNZABCUOZUPQZFGGUQSURUYFUYPUYOUSZUYQUTZUYQUYOVGZUAULZUBULZUYOS
        UCULZUYOSUYTVUAVUBUYOSUYOSRZUCUYQVAZUBUYQVAZUAUYQVAZUDULZUYTUYOSZUYTRZV
        UAUYTUYOSZVUGRZUBUYQVBZTZUAUYQVAZUDUYQVBZUNZFUYQRZUYFVUPUYFDULZJIKULZJU
        LZESZLULZESZVCZRZLIVBKIVBZDVDZUYNBVEAVEZCVDZFUYQUYFVVGUYHUYJVURUYLRZUNZ
        BVEAVEZDVDVVIUYFVVFVVLDUYFVVFVVLVVFUYFVVLVVEUYFVVLVHKLIIVUSIQZVVBIQZTZV
        VEUYFVVLVVOVVEUYFUNZVVJBFVBZAFVBZVVLVVPVVDFQZVURVVDUYIUMZRZBFVBZVVRVVOU
        YFVVSVVEVVOUYFTZVVDVVGFVWCVVDVURJIVUGVUTESZUEULZESZVCZRZUEIVBUDIVBZDVDZ
        VVGVWCVVDVWJQZVVDVWGRZUEIVBUDIVBZVVOVWMUYFVVMVVNVVDVVDRZVWMVVDVFZVWLVWN
        VVDJIVVAVWEESZVCZRUDUEVUSVVBIIVUGVUSRZVWGVWQVVDVWRJIVWFVWPVWRVWDVVAVWEE
        VUGVUSVUTEVIVJVKVLVWEVVBRZVWQVVDVVDVWSJIVWPVVCVWEVVBVVAEVRVKVLVMVNVOVWC
        VVDVPQZVWKVWMVSVWCIVPQZVWTUYFVXAVVOUYFEUPQZVXAEHNVQZEIMVTWAWBJIVVCVPWCW
        AVWIVWMDVVDVPVVEVWHVWLUDUEIIVURVVDVWGWDWEWFWAWGVVFVWIDVVEVWHVURJIVWDVVB
        ESZVCZRKLUDUEIIVUSVUGRZVVDVXEVURVXFJIVVCVXDVXFVVAVWDVVBEVUSVUGVUTEVIVJV
        KVLVVBVWERZVXEVWGVURVXGJIVXDVWFVVBVWEVWDEVRVKVLWIWJWHPWHWKVVPJIVUTVCZFQ
        VURVVDVXHUMZRZVWBVVPVXHVVGFVVPVXHVVGQZVXHVVDRZLIVBKIVBZVVOUYFVXMVVEUYFV
        VOVXMUYFHWLWMZUPQZVVOVXMVHZVXNHVXNVFVQVXNWNWMZVXNUSZQVXOVXPVXQVXNVXRVXR
        VFVXQVFWOVXOVXPVHZVXQIVXRVXQIQZVXSVXOVVOVXTVXTVXMVXOVVOVXHJIVXQVUTESZVX
        QESZVCZRZVXTVXTVXMVHVHVXOVYDVVOVXOJIVUTVYBVXOVUTIQZTZVYBVYAVUTVYFVXBVYA
        IQZTZVYBVYARVXOVXBVYEVYHVXNEUPEVXNNWPZWQZVXBVYETZVXBVYGVXBVYEWRZVYKVXBV
        XTVYEVYGVYLEVXNRZVYKVXTVHNVYKVXTVYMEWNWMZIQZVXBVYOVYEVYNEIMVYNVFWOZVOVY
        MVXQVYNIVYMVYNVXQEVXNWNWTWSXAXBXCVXBVYEXDVXQVUTEIMXEXFXGXHVYAVXQEIMVXNE
        WNVYIXIZUUCWAVXOVXBVYEVYAVUTRVYJVUTVXQEIMVYQUUDXHUUEUUMUUAVXTVXTVYDVXMV
        XTVXTVYDVXMVXLVYDVXHJIVYAVVBESZVCZRKLVXQVXQIIVUSVXQRZVVDVYSVXHVYTJIVVCI
        VYRVYTIXJVYTVVAVYAVVBEVUSVXQVUTEVIVJXKVLVVBVXQRZVYSVYCVXHWUAJIVYRIVYBWU
        AIXJVVBVXQVYAEVRXKVLVMXLXMUUBUUFUUGVXREUSZIVXNEVYIUUHMUUIXNXOWAUUJWKVVP
        VXHVPQZVXKVXMVSUYFVVOWUCVVEUYFVXAWUCUYFIWUBVPMUYFVXBWUBVPQVXCEUPUUKWAUU
        LJIVUTVPWCWAXPVVFVXMDVXHVPVURVXHRVVEVXLKLIIVURVXHVVDWDWEWFWAWGPWHVVPVUR
        VVDVXIVVOVVEUYFUUNVVPIIVVDVGZVXIVVDRVVOUYFWUDVVEVWCJIVVCIVVDVWCVYETZVXB
        VVAIQZVVNVVCIQUYFVXBVVOVYEVXCUUOZWUEVXBVVMVYEWUFWUGVVMVVNUYFVYEUUPVWCVY
        EXDVUSVUTEIMXEXFVVMVVNUYFVYEUUQVVAVVBEIMXEXFVWOUURWKJIIVVDUUSWAUVCVWAVX
        JBVXHFUYIVXHRVVTVXIVURUYIVXHVVDUUTVLXQXRVVQVWBAVVDFUYGVVDRZVVJVWABFWUHU
        YLVVTVURUYGVVDUYIUVAVLYAXQXRVVRUYHUYJTZVVJTZBVEAVEVVLVVJABFFUVBWUJVVKAB
        VVKWUJUYHUYJVVJXSUVDUVEUVMXTXLYBYCUYFVVKVVFABVVKUYFVVFUYHUYJVVJUYFVVFVH
        ZUYJVVJWUKVHZVHZUYGVVGFUYGVVGQWUHLIVBZKIVBZWUMVVFWUODUYGAYDVURUYGRVVEWU
        HKLIIVURUYGVVDWDWEYEWUOUYGJIUFULZVUTESZUGULZESZVCZRZUGIVBZUFIVBZWUMWUNW
        VBKUFIVUSWUPRZVVNWUHTZLVEWURIQZWVATZUGVEWUNWVBWVDWVEWVGLUGWVDUGYFWVDWVE
        UGYLWVDVVBWURRZWVEWVGVSWVDWVHTZVVNWVFWUHWVAWVIVVBWURIWVDWVHXDZXAWVIVVDW
        UTUYGWVIJIVVCWUSWVIVVAWUQVVBWUREWVIVUSWUPVUTEWVDWVHWRVJWVJUVFVKVLYGYHYI
        WUHLIYJWVAUGIYJYKYMUYJWVCWULUYJUYIJIUHULZVUTESZUIULZESZVCZRZUIIVBZUHIVB
        ZWVCWULVHZUYJUYIVVGQUYIVVDRZLIVBZKIVBZWVRFVVGUYIPUVGVVFWWBDUYIBYDVURUYI
        RVVEWVTKLIIVURUYIVVDWDWEYEWWAWVQKUHIVUSWVKRZVVNWVTTZLVEWVMIQZWVPTZUIVEW
        WAWVQWWCWWDWWFLUIWWCUIYFWWCWWDUIYLWWCVVBWVMRZWWDWWFVSWWCWWGTZVVNWWEWVTW
        VPWWHVVBWVMIWWCWWGXDXAWWHVVDWVOUYIWWHJIVVCWVNWWCWWGVVAWVLVVBWVMEVUSWVKV
        UTEVIWWGUVHUVIVKVLYGYHYIWVTLIYJWVPUIIYJYKYMUVJWVPWVSUHUIIIWVCWVKIQZWWET
        ZWVPWULWVAWWJWVPWULVHZVHUFUGIIWUPIQZWVFTZWWJWVAWWKWWMWWJWVAWWKVHWVAWVPV
        VJWWMWWJTZWUKWVAWVPVVJWWNWUKVHZVHWVAWVPTZVVJVURWUTWVOUMZRZWWOWWPUYLWWQV
        URWWPUYGWUTUYIWVOWVAWVPWRWVAWVPXDUVKVLWWRWWNUYFVVFWWRWWNUYFUNZWUPWVKESZ
        IQZWVMWURESZIQZVURJIWWTVUTESZWXBESZVCZRZVVFWWSVXBWWLWWIWXAUYFWWRVXBWWNV
        XCXPZWWLWVFWWJWWRUYFUVLWWIWWEWWMWWRUYFUVNWUPWVKEIMXEXFWWSVXBWWEWVFWXCWX
        HWWIWWEWWMWWRUYFUVOWWLWVFWWJWWRUYFUWCWVMWUREIMXEXFWWRWWNUYFWXGWWNUYFWWR
        WXGWWNUYFWWRWXGVHWWNUYFTZWWRWXGWXIWWQWXFVURWXIVXBWWLWWITZWVFWWETZWWQWXF
        RUYFVXBWWNVXCWBWWNWXJUYFWWMWWLWWJWWIWWLWVFWRWWIWWEWRYNVOWWNWXKUYFWWMWVF
        WWJWWEWWLWVFXDWWIWWEXDYNVOJUJWUPWVKWURWVMWVOWUTEIJUJIWUSWUPUJULZESZWURE
        SVUTWXLRWUQWXMWUREVUTWXLWUPEVRVJYOWVOVFMUVPXFVLUVQYHXMYPVVEWXGVURJIWXDV
        VBESZVCZRKLWWTWXBIIVUSWWTRZVVDWXOVURWXPJIVVCWXNWXPVVAWXDVVBEVUSWWTVUTEV
        IVJVKVLVVBWXBRZWXOWXFVURWXQJIWXNWXEVVBWXBWXDEVRVKVLVMXFXLUVRYHUVSYHUVTY
        BUWAYBYQYCYQYQPXNYPYCUWBUWDUWEVVLVVHDCVURUYKRZVVKUYNABWXRVVJUYMUYHUYJVU
        RUYKUYLWDUWFUWGUWHUWLPUYNABCUWIUWJUYFVUPVUQFFUTZFUYOVGZVUCUCFVAZUBFVAZU
        AFVAZVUIVUKUBFVBZTZUAFVAZUDFVBZUNZUYFWXTWYCWYGUYFUYLFQBFVAAFVAWXTABDEFG
        HIJKLMNOPUWKABCFFUYLFUYOUYNWUIUYMTABCUYHUYJUYMXSUWMZUXAXTABCUAUBUCDEFGH
        IJKLMNOPUWNUYFAIVYNUYGESZVYNESZVCZFQZWYGUYFWYLVVGFUYFWYLVVDRZLIVBKIVBZW
        YLVVGQUYFVYOVYOWYLJIVYNVUTESZVYNESZVCZRZWYOUYFVXBVYOVXCVYPWAZWYTWYSUYFA
        JIWYKWYQUYGVUTRWYJWYPVYNEUYGVUTVYNEVRVJYOYRWYNWYSWYLJIWYPVVBESZVCZRKLVY
        NVYNIIVUSVYNRZVVDXUBWYLXUCJIVVCIXUAXUCIXJXUCVVAWYPVVBEVUSVYNVUTEVIVJXKV
        LVVBVYNRZXUBWYRWYLXUDJIXUAIWYQXUDIXJVVBVYNWYPEVRXKVLVMXFVVFWYODWYLAIWYK
        IWUBVPMEEVXNVPNHWLUWOYSUWPYSZUWQVURWYLRVVEWYNKLIIVURWYLVVDWDWEYEUWRPWHU
        YFWYMTZWYLUYTUYOSZUYTRZVUJWYLRZUBFVBZTZUAFVAZWYGXUFXUHUAFVAXUJUAFVAZXUL
        ABCUADEFGHIJKLMNOPUWSUYFXUMWYMABCUAUBDEFGHIJKLMNOPUWTVOXUHXUJUAFUXBUXCU
        YFWYFXULUDWYLFVUGWYLRZWYFXULVSUYFXUNWYEXUKUAFXUNVUIXUHWYDXUJXUNVUHXUGUY
        TVUGWYLUYTUYOVIUXDXUNVUKXUIUBFVUGWYLVUJUXEYAYGUXFWBUXGUXKUXHUXIVUPWYHVS
        UYQFUYQFRZUYSWXTVUFWYCVUOWYGUYRWXSRZXUOUYSWXTVSXUOXUPUYQFUYQFUXJUXLUYRU
        YQWXSFUYOUXMUXNVUEWYBUAUYQFVUDWYAUBUYQFVUCUCUYQFUXOYTYTVUNWYFUDUYQFVUMW
        YEUAUYQFXUOVULWYDVUIVUKUBUYQFUXPUXQYTUXRUXSUXTXBXOUYFUYOVPQZUYPVUPVSUYF
        FVPQZXURTZXUQXUSUYFXURXURFVVGVPPKLDIIVVDXUEXUEUYAYSZXUTUYBYRABCFFUYLVPV
        PUYOWYIUYCWAUAUBUCUDVPUYOUYQUYQVFUYDWAWGDEFGHIJKLMNOPUYEXG $.
        $( [23-Aug-2014] $) $( [17-Jul-2012] $)
    $}
  $}

  $c opfn $.

  $( Extend class notation with an operator that derives an operation on
     functions from an operation on the elements of the common range of those
     functions. $)
  copfn $a class opfn $.


  ${
    $d g i h x y z a b $.
    $( Multiplication or addition of two functions ` x ` and ` y ` derived from
       the operation ` g ` on the elements of the common range of ` x ` and
       ` y ` .  The functions ` x ` and ` y ` must also have the same domain
       ` i ` . $)
    df-opfn $a |- opfn = { <. <. g , i >. , h >. |
       h = { <. <. x , y >. , z >. |
         ( x e. ( dom dom g ^m i ) /\ y e. ( dom dom g ^m i ) /\
       z = { <. a , b >. | ( a e. dom x /\ b = ( ( x ` a ) g ( y ` a ) ) ) } )
        }
        } $.
  $}

