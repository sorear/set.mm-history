$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Cartesian Products
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the projection symbol. $)
  $c pr $.

  $( Extend class notation to include the projection mapping. $)
  cpro $a class pr $.

  $( Declare the projection symbol. $)
  $c prj $.

  $( Extend class notation to include the projection mapping. $)
  cproj $a class prj $.

  ${
    $d x y f $.
    $( Definition of a projection (also called a coordinate mapping).
       Meaninful if ` x ` is a cartesian product and ` y ` is an index.

       In what follows I will call nuple an element of a cartesian product.

       If ` X ` is a cartesian product, ` N ` a nuple of ` X ` , ` I ` an
       indice, ` ( ( X pr I ) `` N ) ` is the ` I ` th coordinate of the nuple
       ` N ` .

       Suppose the set of indices is ` { 1 , 2 } ` and ` X ` is the cartesian
       product
       ` { { <. 1 , a >. , <. 2 , u >. } , { <. 1 , a >. , <. 2 , v >. } } `
       then ` ( ( X pr 1 ) `` { <. 1 , a >. , <. 2 , u >. } ) = a ` and
       ` ( ( X pr 2 ) `` { <. 1 , a >. , <. 2 , u >. } ) = u ` . $)
    df-pro $a |- pr = ( x e. _V , y e. _V |-> ( f e. x |-> ( f ` y ) ) ) $.
    $( Definition of a projection.  Meaninful if ` x ` is a cartesian product
       and ` y ` is a set of indices.  Suppose ` X = { { <. 1 , a >. , <. 2 , `
       ` u >. } , { <. 1 , a >. , <. 2 , v >. } } ` then
       ` ( X prj 1 ) = { a } ` and ` ( X prj 2 ) = { u , v } ` . $)
    df-prj $a |- prj = ( x e. _V , y e. _V |-> ( f e. x |-> ( f |` y ) ) ) $.
  $}

  ${
    $d A f $.  $d B f $.  $d F f $.  $d f x $.
    $( A nuple is a function.  (Contributed by FL, 6-Jun-2011.) $)
    elixp2a $p |- ( F e. X_ x e. A B -> F Fn A ) $=
      ( vf cv wfn cixp fneq1 wcel cvv cfv wral elixp2 simp2bi vtoclga ) EFZBGZD
      BGEDABCHZBQDIQSJQKJRAFQLCJABMABCQNOP $.
      $( [31-May-2014] $) $( [6-Jun-2011] $)
  $}

  ${
    $d A f $.  $d B f $.  $d F f x $.
    $( The base class of the elements of a nuple.  (Contributed by FL,
       6-Jun-2011.) $)
    elixp2b $p |- ( F e. X_ x e. A B -> A. x e. A ( F ` x ) e. B ) $=
      ( cixp wcel cvv wfn cv cfv wral elixp2 simp3bi ) DABCEFDGFDBHAIDJCFABKABC
      DLM $.
      $( [31-May-2014] $) $( [6-Jun-2011] $)
  $}

  ${
    $d A x $.  $d C x $.  $d F x $.  $d I x $.
    bcelnu.1 $e |- ( x = I -> B = C ) $.
    $( The base class of an element of a nuple.  (Contributed by FL,
       19-Jun-2011.) $)
    bclelnu $p |- ( ( F e. X_ x e. A B /\ I e. A ) -> ( F ` I ) e. C ) $=
      ( cixp wcel cv cfv wral elixp2b wceq fveq2 eleq12d rcla4cva sylan ) EABCH
      IAJZEKZCIZABLFBIFEKZDIZABCEMUAUCAFBSFNTUBCDSFEOGPQR $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d I f g x y z $.  $d P f g x y z $.
    $( Definition of the coordinate mapping (or projection ) of index ` I ` .
       ` P ` is a cartesian product.  (Contributed by FL, 19-Jun-2011.) $)
    ispr1 $p |- ( ( P e. V /\ I e. W ) -> ( P pr I ) =
                  ( f e. P |-> ( f ` I ) ) ) $=
      ( vx vy wcel wa cvv cv cfv cmpt cpro co wceq elex adantr adantl mptexg
      mpteq1 fveq2 mpteq2dv df-pro ovmpt2g syl3anc ) ADHZCEHZIAJHZCJHZBACBKZLZM
      ZJHZACNOUMPUGUIUHADQRUHUJUGCEQSUGUNUHBAULDTRFGACJJBFKZGKZUKLZMUMNBAUQMJBU
      OAUQUAUPCPBAUQULUPCUKUBUCFGBUDUEUF $.
      $( [23-Aug-2014] $) $( [19-Jun-2011] $)
  $}

  ${
    $d f x A $.  $d f x C $.  $d f x I $.  $d f P $.  $d f V $.
    prmapcp2.1 $e |- P = X_ x e. A B $.
    prmapcp2.2 $e |- ( x = I -> B = C ) $.
    $( A projection is a mapping from a cartesian product to an element of the
       family implied in the product.  Bourbaki E.II.34 cor. 1.  (Contributed
       by FL, 19-Jun-2011.) $)
    prmapcp2 $p |- ( ( P e. V /\ I e. A ) -> ( P pr I ) : P --> C ) $=
      ( vf wcel wa cpro co wf cv cfv cmpt cixp eleq2i biimpi simpr bclelnu eqid
      syl2anr fmptd ispr1 feq1d mpbird ) EGKZFBKZLZEDEFMNZOEDJEFJPZQZRZOULJEUOD
      UPUNEKZUNABCSZKZUKUODKULUQUSEURUNHTUAUJUKUBABCDUNFIUCUEUPUDUFULEDUMUPEJFG
      BUGUHUI $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d F f g $.  $d I f g $.  $d P f g $.
    $( The ` I ` th coordinate of the nuple ` F ` .  (Contributed by FL,
       19-Jun-2011.) $)
    valpr $p |- ( ( P e. V /\ I e. W /\ F e. P )
      -> ( ( P pr I ) ` F ) = ( F ` I ) ) $=
      ( vf wcel cpro co cfv wceq wa cv cmpt ispr1 fveq1d fveq1 eqid fvex fvmpt
      sylan9eq 3impa ) ADGZCEGZBAGZBACHIZJZCBJZKUCUDLZUEUGBFACFMZJZNZJUHUIBUFUL
      AFCDEOPFBUKUHAULCUJBQULRCBSTUAUB $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d f x A $.  $d f x F $.  $d f x P $.  $d f x Q $.
    npincppr.1 $e |- P = X_ x e. A B $.
    $( A set of nuples is included in the cartesian product of the projections
       of the nuples.  Bourbaki E.II.32.  (Contributed by FL, 20-Jun-2011.)
       (The proof was shortened by Mario Carneiro, 31-May-2014.) $)
    npincppr $p |- ( ( F C_ P /\ P e. Q )
      -> F C_ X_ x e. A ( ( P pr x ) " F ) ) $=
      ( vf wss wcel wa cv cpro cixp wfn cfv wceq cvv syl2anc syl cima wral ciun
      co wf ssel2 adantlr syl6eleq ixpf ffn simplr simprr adantrr valpr syl3anc
      3syl simprl wfun cdm wi cmpt eqid fnmpt fvex a1i mprg ispr1 fneq1d mpbiri
      fnfun eleqtrrd funfvima mpd eqeltrrd anassrs ralrimiva vex elixp sylanbrc
      fndm ex ssrdv ) FDIZDEJZKZHFABDALZMUDZFUAZNZWEHLZFJZWJWIJZWEWKKZWJBOZWFWJ
      PZWHJZABUBWLWMWJABCNZJBABCUCZWJUEWNWMWJDWQWCWKWJDJZWDFDWJUFUGZGUHABCWJUIB
      WRWJUJUPWMWPABWEWKWFBJZWPWEWKXAKZKZWJWGPZWOWHXCWDXAWSXDWOQWCWDXBUKZWEWKXA
      ULZWEWKWSXAWTUMZDWJWFEBUNUOXCWKXDWHJZWEWKXAUQXCWGURZWJWGUSZJWKXHUTXCWGDOZ
      XIXCXKHDWOVAZDOZWORJZXMHDHDWOXLRXLVBVCXNWSWFWJVDVEVFXCDWGXLXCWDXAWGXLQXEX
      FDHWFEBVGSVHVIZDWGVJTXCWJDXJXGXCXKXJDQXODWGVTTVKFWJWGVLSVMVNVOVPABWHWJHVQ
      VRVSWAWB $.
      $( [31-May-2014] $) $( [20-Jun-2011] $)
  $}

  ${
    $d F x $.  $d I x $.  $d J x $.
    repfuntw.1 $e |- I e. A $.
    repfuntw.2 $e |- J e. B $.
    $( Representation as a set of pairs of a function whose domain has two
       distinct elements.  (Contributed by FL, 26-Jun-2011.) $)
    repfuntw $p |- ( I =/= J -> ( F Fn { I , J } <->
       F = { <. I , ( F ` I ) >. , <. J , ( F ` J ) >. } ) ) $=
      ( vx cfv wceq wa wi csn cun cima cab cuni c0 cin a1i wne cpr wfn cop wral
      cv wcel wo elprg cdm dmsnop ineq1 necom disjsn2 sylbi syl5ibr mp2b adantr
      eqeq1 imadisj sylibr uneq2 un0 syl6eq eqeq1d abbidv elexi fvex fvsn df-fv
      unieqd eqtr3i syl6reqr syl eqcomi eqeq1i abbii unieqi df-pr fveq1d 3eqtrd
      imaundir fveq2 eqeq12d incom uncom imaeq1i jaoi pm2.43i com12 ralrimiv wb
      syl6bi simpr wfun funpr dmprop jctir df-fn eqfnfv2 syl2anc mpbird ineq12d
      eqtr eqtrd funsn pm3.2i jctil funun funeq biimpd eqcoms 3syl mpan2 impcom
      syl5com dmeq adantl sylanbrc impbida ) DEUAZCDEUBZUCZCDDCIZUDZEECIZUDZUBZ
      JZYAYCKZYIHUFZCIZYKYHIZJZHYBUEZYJYNHYBYKYBUGZYJYNYPYJYNLZYPYPYKDJZYKEJZUH
      YQYKDEYBUIYRYQYSYJYNYRYDDYHIZJYJYDYEMZYGMZNZDMZOZYKMZJZHPZQZDUUCIZYTYJYDU
      UAUUDOZUUBUUDOZNZUUFJZHPZQZUUIYJUULRJZYDUUPJYJUUBUJZUUDSZRJZUUQYAUUTYCUUR
      EMZJZUUSUVAUUDSZJZYAUUTLEYFUKZUURUVAUUDULYAUUTUVDUVCRJZYAEDUAUVFDEUMEDUNU
      OZUUSUVCRUSUPUQURUUBUUDUTVAUUQUUPUUKUUFJZHPZQZYDUUQUUOUVIUUQUUNUVHHUUQUUM
      UUKUUFUUQUUMUUKRNUUKUULRUUKVBUUKVCVDVEVFVKDUUAIYDUVJDYDDAFVGZDCVHVIHDUUAV
      JVLVMVNUUOUUHUUNUUGHUUMUUEUUFUUEUUMUUAUUBUUDWBVOVPVQVRVDUUIUUJJYJUUJUUIHD
      UUCVJVOTYJDUUCYHUUCYHJYJYHUUCYEYGVSZVOTZVTWAYRYLYDYMYTYKDCWCYKDYHWCWDUPYJ
      YNYSYFEYHIZJYJYFUUCUVAOZUUFJZHPZQZEUUCIZUVNYJYFUUBUVAOZUUAUVAOZNZUUFJZHPZ
      QZUUBUUANZUVAOZUUFJZHPZQZUVRYJUWARJZYFUWEJYJUUAUJZUVASZRJZUWKYAUWNYCUWLUU
      DJZUWMUVCJZYAUWNLDYDUKZUWOUWMUUDUVASZUVCUWLUUDUVAULUUDUVAWEVDYAUWNUWPUVFU
      VGUWMUVCRUSUPUQURUUAUVAUTVAUWKUWEUVTUUFJZHPZQZYFUWKUWDUWTUWKUWCUWSHUWKUWB
      UVTUUFUWKUWBUVTRNUVTUWARUVTVBUVTVCVDVEVFVKEUUBIYFUXAEYFEBGVGZECVHVIHEUUBV
      JVLVMVNUWEUWJJYJUWDUWIUWCUWHHUWBUWGUUFUWGUWBUUBUUAUVAWBVOVPVQVRTUWJUVRJYJ
      UWIUVQUWHUVPHUWGUVOUUFUWFUUCUVAUUBUUAWFWGVPVQVRTWAUVRUVSJYJUVSUVRHEUUCVJV
      OTYJEUUCYHUVMVTWAYSYLYFYMUVNYKECWCYKEYHWCWDUPWHWMWIWJWKYJYCYHYBUCZYIYOWLY
      AYCWNYAUXCYCYAYHWOZYHUJZYBJZKUXCYAUXDUXFDEYDYFUVKUXBWPDEYDYFWQZWRYHYBWSVA
      URHYBCYHWTXAXBYAYIKCWOZCUJZYBJZYCYIYAUXHYIYHUUCJZYAUXHLUVLYIUXKKCUUCJZYAU
      XHCYHUUCXDYAUUAWOZUUBWOZKZUWLUURSZRJZKUUCWOZUXLUXHLYAUXQUXOYAUXPUWRRYAUWL
      UUDUURUVAUWOYAUWQTUVBYAUVETXCDEUNXEUXMUXNDYDUVKXFEYFUXBXFXGXHUUAUUBXIUXLU
      XRUXHUXRUXHLUUCCUUCCJUXRUXHUUCCXJXKXLWJXMXPXNXOYIUXJYAYIUXIUXEYBCYHXQUXGV
      DXRCYBWSXSXT $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d x y M $.  $d x A f a b $.  $d x f I a b $.  $d x f J a b $.  $d x y N $.
    $d B f a b $.  $d M a b $.  $d N a b $.
    repcpwti.1 $e |- A = { I , J } $.
    repcpwti.2 $e |- B = if ( x = I , M , N ) $.
    repcpwti.3 $e |- I e. C $.
    repcpwti.4 $e |- J e. D $.
    $( A representation of a cartesian product with two indices.  (Contributed
       by FL, 26-Jun-2011.) $)
    repcpwti $p |- ( I =/= J -> X_ x e. A B = { f | E. a e. M E. b e. N
                          f = { <. I , a >. , <. J , b >. } } ) $=
      ( wcel wceq wi cvv vy wne cixp cv wfn cfv wral wa cab cop cpr wrex df-ixp
      wsbc raleqi elexi ralpr bitri csb sbcel12g biimpd ax-mp csbfvg cif iftrue
      ax-17 syl5eq csbief eleq12i wb a1i wal neeq2 eqcoms wn df-ne eqcom sylnbi
      iffalse sylbi syl6com alrimiv csbieb sylib eleq12d repfuntw biimpcd opeq2
      fneq2i preq2d eqeq2d rcla4ev preq1d adantr rexbidva ex expcom com3r com34
      syl5com syl6bi pm2.43a com4l com12 syl impcom fnprg 3exp com13 com3l 3imp
      imp w3a sylibr mpan imp31 wo elpr fvsn2a eleq1a syl5 eleq2 syl5ibr eleq2i
      syl6ibr fveq2 eleq1d sylibrd fvsn2b necom simpr syl6bir eleq1 imbi2d jaoi
      vex mpcom eleq2s ralrimiv jca ralbidv anbi12d syl5ibrcom rexlimivv impbid
      fneq1 fveq1 abbidv ) GHUBZABCUCFUDZBUEZAUDZUUJUFZCQZABUGZUHZFUIUUJGKUDZUJ
      ZHLUDZUJZUKZRZLJULZKIULZFUIABCFUMUUIUUPUVDFUUIUUPUVDUUPUUIUVDUUOUUKUUIUVD
      SZUUOUUNAGUNZUUNAHUNZUHZUUKUVESZUUOUUNAGHUKZUGUVHUUNABUVJMUOUUNAGHGDOUPZH
      EPUPZUQURUVFUVGUVIUVFAGUUMUSZAGCUSZQZUVGUVISZGTQZUVFUVOSUVKUVQUVFUVOAGUUM
      CTUTVAVBUVOGUUJUFZIQZUVPUVMUVRUVNIUVQUVMUVRRUVKAGTUUJVCVBAUAGCIUVKUAUDZIQ
      AVFUULGRZCUWAIJVDZINUWAIJVEZVGVHVIUVGUVSUVIUVGAHUUMUSZAHCUSZQZUVSUVISHTQZ
      UVGUWFVJUVLAHUUMCTUTVBUUIUWFUVSUUKUVDUWFUUIUVSUUKUVDSSZUUIUWFHUUJUFZJQZUU
      IUWHSUUIUWDUWIUWEJUWDUWIRZUUIUWGUWKUVLAHTUUJVCVBVKUUIUULHRZCJRZSZAVLUWEJR
      UUIUWNAUWLUUIGUULUBZUWMUWLUUIUWOUUIUWOVJHUULHUULGVMVNVAUWOGUULRZVOZUWMGUU
      LVPUWQCUWBJNUWPUWAUWBJRZGUULVQUWAIJVSZVRVGVTWAWBAUAHCJUVLUVTJQAVFWCWDWEUW
      JUUIUUKUVSUVDUUIUUKUWJUVSUVDSZUUKUUIUUJGUVRUJZHUWIUJZUKZRZUWJUWTSUUKUUJUV
      JUEZUUIUXDSBUVJUUJMWIUUIUXEUXDDEUUJGHOPWFWGVTUWJUXDUWTUWJUXDUHUUJUXAUUTUK
      ZRZLJULZUVSUVDUXGUXDLUWIJUUSUWIRZUXFUXCUUJUXIUUTUXBUXAUUSUWIHWHWJWKWLUVSU
      XHUVDUVCUXHKUVRIUUQUVRRZUVBUXGLJUXJUUSJQZUHUVAUXFUUJUXJUVAUXFRUXKUXJUURUX
      AUUTUUQUVRGWHWMWNWKWOWLWPWTWQWAWRWSXAXBXCVTXDVTXEXLVTXFXDUVDUUIUUPUVBUUIU
      UPSZKLIJUUQIQZUXKUHZUXLUVBUUIUVABUEZUULUVAUFZCQZABUGZUHZSUXNUUIUXSUXNUUIU
      HZUXOUXRUXMUXKUUIUXOUVQUXMUXKUUIUXOSZSUVKUXKUVQUXMUHZUYAUWGUXKUYBUYASUVLU
      UIUYBUWGUXKUHZUXOUUIUYBUYCUXOUUIUYBUYCXMUVAUVJUEZUXOUUIUYBUYCUYDUYBUUIUYC
      UYDSZUVQUUIUYESUXMUYCUVQUUIUYDUWGUVQUUIUYDSSUXKUUIUVQUWGUYDUUIUVQUWGUYDGH
      UUQUUSTTXGXHXIWNXJWNXDXKBUVJUVAMWIXNXHXIXOXDXOXPUXTUXQABUULBQUXTUXQUXTUXQ
      SZUULUVJBUULUVJQUWAUWLXQUYFUULGHAYPXRUWAUYFUWLUWAUXTGUVAUFZCQZUXQUWAUXTUY
      GUWBQZUYHUWAUWBIRZUXTUYISUWCUXTUYIUYJUYGIQZUXNUUIUYKUUIUYGUUQRZUXNUYKGHUU
      QUUSDETTOPKYPZLYPZXSUXMUYLUYKSUXKUUQIUYGXTWNYAXLUWBIUYGYBYCXECUWBUYGNYDYE
      UWAUXPUYGCUULGUVAYFYGYHUWLUXTHUVAUFZCQZUXQUWLUXTUYOUWBQZUYPUXTUWLUYQUUIUX
      NUWLUYQSZUYOUUSRZUUIUXNUYRSZGHUUQUUSDETTOPUYMUYNYIUUIUYTUYSUXNUWLUUSUWBQZ
      SZSUWLUUIUXNVUAUWLUUIUWOUXNVUASZUULHGVMUWOUULGUBZVUCGUULYJVUDUWAVOZVUCUUL
      GVPVUEUWRVUCUWSUXNVUAUWRUXKUXMUXKYKUWBJUUSYBYCXEVTVTYLXJUYSUYRVUBUXNUYSUY
      QVUAUWLUYOUUSUWBYMYNYNYCYQXFXDCUWBUYONYDYEUWLUXPUYOCUULHUVAYFYGYHYOVTMYRX
      DYSYTWPUVBUUPUXSUUIUVBUUKUXOUUOUXRBUUJUVAUUFUVBUUNUXQABUVBUUMUXPCUULUUJUV
      AUUGYGUUAUUBYNUUCUUDXDUUEUUHVG $.
      $( [26-Jun-2011] $)
  $}

  ${
    $( Warning: a b x distincts seem curious. $)
    $d A a b f x $.  $d B a b f $.  $d F c d e f y $.  $d F e f x y $.
    $d I a b c d e f $.  $d I a b e f x $.  $d I c d e f y $.
    $d J a b c d e f $.  $d J a b e f x $.  $d J c d e f y $.
    $d M a b c d e f $.  $d M a b e f x $.  $d M c d e f y $.
    $d N a b c d e f $.  $d N a b e f x $.  $d N c d e f y $.
    cbcpcp.1 $e |- A = { I , J } $.
    cbcpcp.2 $e |- B = if ( x = I , M , N ) $.
    cbcpcp.3 $e |- F = ( a e. M , b e. N |-> { <. I , a >. , <. J , b >. } ) $.
    cbcpcp.4 $e |- I e. C $.
    cbcpcp.5 $e |- J e. D $.
    $( The canonical bijection between a cross product and a cartesian product
       (whose set of indices is composed of two different elements).  Bourbaki
       E.II.33 .  (Contributed by FL, 26-Jun-2011.) $)
    cbcpcp $p |- ( I =/= J -> F : ( M X. N ) -1-1-onto-> X_ x e. A B ) $=
      ( wceq wi wcel vy vf ve vc wne cxp wfn crn cixp cfv wral wf1o cop cpr cvv
      vd cv prex rgen2w a1i fnmpt2 sylib wrex cab repcpwti rnmpt2 syl6reqr elxp
      wa wex co w3a df-ov opeq2 preq1d preq2d ovmpt2g eqtr wo preq12b vex opth2
      opex opeq12 expcom a1dd syl mpan9 elexi opth1 df-ne pm2.21 syl5com adantr
      wn sylbi jaoi com12 eqeq2 imbi1d syl5ibr eqcoms mpsyl mp3an3 eqeq1 imbi2d
      ex 3expia com3l ax-mp eqeq2d imbi12d imp exlimivv eqeq1d ralrimivv dff1o6
      fveq2 syl3anbrc ) GHUEZFIJUFZUGZFUHZABCUIZRAUQZFUJZUAUQZFUJZRZYEYGRZSZUAY
      AUKAYAUKYAYDFULXTGKUQZUMZHLUQZUMZUNZUOTZLJUKKIUKZYBYRXTYQKLIJYMYOURUSUTKL
      IJYPFOVAVBXTYDUBUQZYPRLJVCKIVCUBVDYCABCDEUBGHIJKLMNPQVEKLUBIJYPFOVFVGXTYK
      AUAYAYAYEYATZYGYATZVIXTYKYTUUAXTYKSZYTYEUCUQZYSUMZRZUUCITZYSJTZVIZVIZUBVJ
      UCVJUUAUUBSZUCUBYEIJVHUUIUUJUCUBUUEUUHUUJUUHUUJUUEUUAXTUUDFUJZYHRZUUDYGRZ
      SZSZSUUAUUHUUOUUAYGUDUQZUPUQZUMZRZUUPITZUUQJTZVIZVIZUPVJUDVJUUHUUOSZUDUPY
      GIJVHUVCUVDUDUPUUSUVBUVDUVBUVDUUSUUHXTUUKUURFUJZRZUUDUURRZSZSZSZGUUCUMZHY
      SUMZUNZUOTZUVBUVJSUVKUVLURUUHUVNUVBUVIUUFUUGUVNUVBUVISZUUCYSFVKZUUKRUUFUU
      GUVNVLUVPUVMRZUVOUUCYSFVMKLUUCYSIJYPUVMFUVKYOUNUOYLUUCRYMUVKYOYLUUCGVNVOY
      NYSRYOUVLUVKYNYSHVNVPOVQUVQUVOSUUKUVPUUKUVPRZUVQUVOUVRUVQVIUUKUVMRZUVOUUK
      UVPUVMVRUVBUVIUVSXTUVMUVERZUVGSZSZUUTUVAGUUPUMZHUUQUMZUNZUOTZUWBUWCUWDURU
      UPUUQFVKZUVERUUTUVAUWFVLUWGUWERZUWBUUPUUQFVMKLUUPUUQIJYPUWEFUWCYOUNUOYLUU
      PRYMUWCYOYLUUPGVNVOYNUUQRYOUWDUWCYNUUQHVNVPOVQUWHUWBSUVEUWGUVEUWGRZUWHUWB
      UWIUWHVIUVEUWERZUWBUVEUWGUWEVRXTUWAUWJUVMUWERZUVGSUWKXTUVGUWKUVKUWCRZUVLU
      WDRZVIZUVKUWDRZUVLUWCRZVIZVSXTUVGSZUVKUVLUWCUWDGUUCWCHYSWCGUUPWCHUUQWCVTU
      WNUWRUWQUWLUUCUUPRZUWMUWRGUUCGUUPUCWAUDWAWBUWMYSUUQRZUWSUWRSHYSHUUQUBWAUP
      WAWBUWTUWSUVGXTUWSUWTUVGUUCYSUUPUUQWDWEWFWGWHUWOUWRUWPUWOGHRZXTUVGGUUCHUU
      QGDPWIWJXTUXAWOUXAUVGSGHWKUXAUVGWLWPWMWNWQWPWRUWJUVTUWKUVGUVEUWEUVMWSWTXA
      WGXGXBXCXDUVSUVHUWAXTUVSUVFUVTUVGUUKUVMUVEXEWTXFXAWGXGXBXCXHXIXJUUSUUOUVI
      UUHUUSUUNUVHXTUUSUULUVFUUMUVGUUSYHUVEUUKYGUURFXRXKYGUURUUDWSXLXFXFXAXMXNW
      PWRUUEUUBUUOUUAUUEYKUUNXTUUEYIUULYJUUMUUEYFUUKYHYEUUDFXRXOYEUUDYGXEXLXFXF
      XAXMXNWPXMWRXPAUAYAYDFXQXS $.
      $( [26-Jun-2011] $)
  $}

  ${
    $d I f g x y z $.  $d P f g x y z $.
    $( Definition of a projection. ` I ` is a set of indices. ` P ` is a
       cartesian product.  (Contributed by FL, 19-Jun-2011.) $)
    isprj1 $p |- ( ( P e. V /\ I e. W ) -> ( P prj I ) =
                   ( f e. P |-> ( f |` I ) ) ) $=
      ( vx vy wcel wa cvv cv cres cmpt cproj co wceq elex adantr adantl mptexg
      mpteq1 reseq2 mpteq2dv df-prj ovmpt2g syl3anc ) ADHZCEHZIAJHZCJHZBABKZCLZ
      MZJHZACNOUMPUGUIUHADQRUHUJUGCEQSUGUNUHBAULDTRFGACJJBFKZUKGKZLZMUMNBAUQMJB
      UOAUQUAUPCPBAUQULUPCUKUBUCFGBUDUEUF $.
      $( [23-Aug-2014] $) $( [19-Jun-2011] $)
  $}

  ${
    $d A x $.  $d I f g $.  $d P f g $.
    isprj2.1 $e |- P = X_ x e. A B $.
    $( Definition of a projection. ` I ` is a set of indices. ` P ` is a
       cartesian product.  (Contributed by FL, 19-Jun-2011.) $)
    isprj2 $p |- ( ( I e. V /\ A e. W /\ A. x e. A B e. D ) ->
                   ( P prj I ) = ( f e. P |-> ( f |` I ) ) ) $=
      ( wcel wral w3a cvv cproj co cv cres cmpt wceq wa ixpexg syl5eqel 3adant1
      cixp simp1 isprj1 syl2anc ) GHKZBIKZCDKABLZMENKZUIEGOPFEFQGRSTUJUKULUIUJU
      KUAEABCUENJABCIDUBUCUDUIUJUKUFEFGNHUGUH $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d A f x y $.  $d B f y $.  $d C f $.  $d D f $.  $d I f x y $.
    $( A projection is a mapping from a cartesian product to one of its
       restriction.  (Contributed by FL, 19-Jun-2011.) $)
    prjmapcp $p |- ( ( I C_ A /\ A e. C /\ A. x e. A B e. D )
      -> ( X_ x e. A B prj I ) : X_ x e. A B --> X_ x e. I B ) $=
      ( vf wss wcel wral w3a cixp cproj co wf cv cres cmpt eqid cvv resixp wceq
      3ad2antl1 fmptd ssexg 3adant3 isprj2 syld3an1 feq1d mpbird ) FBHZBDIZCEIA
      BJZKZABCLZAFCLZUOFMNZOUOUPGUOGPZFQZRZOUNGUOUSUPUTUKULURUOIUSUPIUMABFCURUA
      UCUTSUDUNUOUPUQUTFTIZULUKUMUQUTUBUKULVAUMFBDUEUFABCEUOGFTDUOSUGUHUIUJ $.
      $( [31-May-2014] $) $( [19-Jun-2011] $)
  $}

  ${
    $d f x y A $.  $d f y B $.  $d f x y C $.  $d f y V $.  $d f x y W $.
    cbicp.1 $e |- ( x = A -> B = C ) $.
    $( Canonical bijection between a cartesian product indexed by a singleton
       and the base class of the elements of the 1-uple.  Bourbaki E II.32
       (Contributed by FL, 6-Jun-2011.)  (The proof was shortened by Mario
       Carneiro, 31-May-2014.) $)
    cbicp $p |- ( ( A e. V /\ C e. W ) ->
                  ( X_ x e. { A } B pr A ) : X_ x e. { A } B -1-1-onto-> C ) $=
      ( vf wcel wa csn wf1o cv cfv syl2anc wral syl wceq wf cvv vy cixp cpro co
      cmpt cop eqid simpr snidg ad2antrr bclelnu wfn f1osng adantlr wsbc ralsng
      f1ofn eqeq1d sbcieg bitrd mpbiri wss snssi adantl ffvelrn ralrimiva eleq2
      f1of fss biimprd ral2imi sylc elixp sylanbrc fvsng ad2ant2rl eqcomd fveq1
      snex eqeq2d syl5ibrcom simpll elixp2a dffn2 sylib sneq feq2d fveq2 eleq1d
      ad2antrl id opeq12d sneqd anbi12d fsn2 vtoclbg biimpa simprd opeq2 impbid
      vex f1o2d wb biimpar ixpexg sylancr simpl ispr1 f1oeq1 mpbird ) BEIZDFIZJ
      ZABKZCUBZDXOBUCUDZLZXODHXOBHMZNZUEZLZXMHUAXODXSBUAMZUFZKZXTXTUGXMXRXOIZJY
      EBXNIZXSDIXMYEUHXKYFXLYEBEUIUJAXNCDXRBGUKOXMYBDIZJZYDXNULZAMZYDNZCIZAXNPZ
      YDXOIYHXNYBKZYDLZYIXKYGYOXLBYBEDUMUNZXNYNYDUQQYHCDRZAXNPZYKDIZAXNPZYMXKYR
      XLYGXKYRDDRZDUGXKYRYQABUOUUAYQABEUPYQUUAABEYJBRZCDDGURUSUTVAUJYHXNDYDSZYT
      YHXNYNYDSZYNDVBZUUCYHYOUUDYPXNYNYDVHQYGUUEXMYBDVCVDXNYNDYDVIOUUCYSAXNXNDY
      JYDVEVFQYQYSYLAXNYQYLYSCDYKVGVJVKVLAXNCYDYCVSVMVNXMYEYGJZJZXRYDRZYBXSRZUU
      GUUIUUHYBBYDNZRUUGUUJYBXKYGUUJYBRXLYEBYBEDVOVPVQUUHXSUUJYBBXRYDVRVTWAUUGU
      UHUUIXRBXSUFZKZRZUUGXSTIZUUMUUGXKXNTXRSZUUNUUMJZXKXLUUFWBUUGXRXNULZUUOYEU
      UQXMYGAXNCXRWCWJXNXRWDWEXKUUOUUPYJKZTXRSYJXRNZTIZXRYJUUSUFZKZRZJUUOUUPABE
      UUBUURXNTXRYJBWFWGUUBUUTUUNUVCUUMUUBUUSXSTYJBXRWHZWIUUBUVBUULXRUUBUVAUUKU
      UBYJBUUSXSUUBWKUVDWLWMVTWNYJTXRAXAWOWPWQOWRUUIYDUULXRUUIYCUUKYBXSBWSWMVTW
      AWTXBXMXPXTRZXQYAXCXMXOTIZXKUVEXMXNTICFIZAXNPZUVFBVSXKUVHXLXKUVHUVGABUOXL
      UVGABEUPUVGXLABEUUBCDFGWIUSUTXDAXNCTFXEXFXKXLXGXOHBTEXHOXODXPXTXIQXJ $.
      $( [31-May-2014] $) $( [6-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.  $d G x $.
    $( Union of two projections of a cartesian product.  (Contributed by FL,
       7-Nov-2011.) $)
    unprj $p |-
    ( ( F e. X_ x e. B C /\ G e. X_ x e. ( A \ B ) C /\ B C_ A )
        -> ( F u. G ) e. X_ x e. A C ) $=
      ( cixp wcel cun wfn wral wi wa c0 wceq sylancl syl syl5com wn eqcoms cdif
      wss w3a cvv cv cfv unexg 3adant3 elixp2a 3simpa ancomd disjdif fnun undif
      cin biimpi eqcomd 3ad2ant3 fneq2d mpbird 3exp 3imp cdm fndm elndif notbid
      wb eleq2 ndmfv syl6bi syl2im ralrimiv elixp2b uneq2 un0 eqtr eleq1 biimpd
      com12 ral2imi impcom eldifn uneq1 uncom imp ralunb sylanbrc raleq syl5ibr
      ex imbi2d sylbi com3l wfun df-fn simpl anim12i ineq12 syl6eq fvun syl2anc
      ad2ant2l eleq1d ralbidv elixp2 syl3anbrc ) EACDGZHZFABCUAZDGZHZCBUBZUCZEF
      IZUDHZXNBJZAUEZXNUFZDHZABKZXNABDGHXHXKXOXLEFXGXJUGUHXHXKXLXPXHECJZXKXLXPL
      ZACDEUIZXKFXIJZYAYBLAXIDFUIZYDYAXLXPYDYAXLUCZXPXNCXIIZJZYFYAYDMCXIUOZNOYH
      YFYDYAYDYAXLUJUKCBULZCXIEFUMPYFBYGXNXLYDBYGOZYAXLYGBXLYGBOZCBUNZUPUQURUSU
      TVAQRVBXMXTXQEUFZXQFUFZIZDHZABKZXHXKXLYRXLXHXKYRXLYLXHXKYRLZLZYMYTBYGXHYS
      YKXKYQAYGKZLXHXKUUAXHXKMYQACKZYQAXIKZUUAXKXHUUBXKYDXHUUBLYEYDYONOZACKZXHU
      UBYDUUDACYDFVCZXIOZXQCHZXQXIHZSZUUDXIFVDXQCBVEUUGUUJXQUUFHZSZUUDUUJUULVGX
      IUUFXIUUFOUUIUUKXIUUFXQVHVFTXQFVIVJVKVLXHYNDHZACKUUEUUBLACDEVMUUMUUDYQACU
      UDUUMYQUUDYPYNNIZOZUUNYNOZUUMYQLZYONYNVNYNVOUUOUUPMYPYNOUUQYPUUNYNVPUUQYN
      YPYNYPOUUMYQYNYPDVQVRTQPVSVTQRQWAXHXKUUCXHYAXKUUCLYCYAYNNOZAXIKZXKUUCYAUU
      RAXIYAEVCZCOZUUIUUHSZUURCEVDXQBCWBUVBUURLCUUTCUUTOZUVBXQUUTHZSUURUVCUUHUV
      DCUUTXQVHVFXQEVIVJTVKVLXKYODHZAXIKUUSUUCLAXIDFVMUVEUURYQAXIUURUVEYQUURYPN
      YOIZOZUVFYONIZOZUVEYQLZYNNYOWCNYOWDUVGUVIMYPUVHOZUVHYOOZUVJYPUVFUVHVPYOVO
      UVKUVLMYPYOOUVJYPUVHYOVPUVJYOYPYOYPOUVEYQYOYPDVQVRTQPPVSVTQRQWEYQACXIWFWG
      WJYKYRUUAXKYQABYGWHWKWITWLWMVBXHXKXLXTYRVGZXHYAXKXLUVMLZYCXKYDYAUVNLZYEYD
      FWNZUUGMZUVOFXIWOYAUVQUVNYAEWNZUVAMZUVQUVNLECWOUVSUVQXLUVMUVSUVQXLUCZXSYQ
      ABUVTXRYPDUVTUVRUVPMZUUTUUFUOZNOZXRYPOUVSUVQUWAXLUVSUVRUVQUVPUVRUVAWPUVPU
      UGWPWQUHUVSUVQUWCXLUVAUUGUWCUVRUVPUVAUUGMUWBYINUUTCUUFXIWRYJWSXBUHXQEFWTX
      AXCXDVAWLVSWLQRVBUTABDXNXEXF $.
      $( [7-Nov-2011] $)
  $}

  ${
    $d A f h x $.  $d B f h x $.  $d B g x $.  $d C f h $.  $d C g $.
    $d G f h $.  $d G g $.  $d G x $.
    prl.1 $e |- A e. D $.
    $( Existence of a "prolongement" of a cartesian product.  Bourbaki E.II.34
       prop. 6.  (Contributed by FL, 7-Nov-2011.) $)
    prl $p |- ( ( A. x e. A C =/= (/) /\ B C_ A /\ G e. X_ x e. B C )
     -> E. f e. X_ x e. A C G C_ f ) $=
      ( vh cv cixp wcel wex c0 wne wss w3a wi a1d exlimiv cdif wral elexi difss
      wrex ac9s n0 wa cres resixp eleq1 cla4egv sylc mpan sylbi imp 3adant3 cun
      unprj ssun1 sseq2 rcla4ev mpan2 syl 3exp com34 com4t 3imp com12 mpcom ) I
      JZABCUAZDKZLZIMZDNOABUBZCBPZGACDKLZQZGFJZPZFABDKZUEZVPVQVOVRVPVQVOVPWBNOZ
      VQVORZABDBEHUCUFWDVTWBLZFMWEFWBUGWFWEFWFVOVQVLBPZWFVOBCUDWGWFUHVTVLUIZVML
      ZWIVOABVLDVTUJZWJVNWIIWHVMVKWHVMUKULUMUNSTUOUOUPUQVNVSWCRIVSVNWCVPVQVRVNW
      CRVRVNVPVQWCVRVNVQVPWCVRVNVQVPWCRZVRVNVQQGVKURZWBLZWKABCDGVKUSWMWCVPWMGWL
      PZWCGVKUTWAWNFWLWBVTWLGVAVBVCSVDVEVFVGVHVITVJ $.
      $( [7-Nov-2011] $)
  $}

  ${
    $d A g $.  $d B f g $.  $d C g $.  $d f g x $.  $d A f $.  $d A x $.
    $d B x $.  $d C f $.
    prl1.1 $e |- A e. D $.
    $( Existence of a "prolongement" of a cartesian product.  Bourbaki E.II.34
       prop. 6.  (Contributed by FL, 20-Nov-2011.) $)
    prl1 $p |- ( ( A. x e. A C =/= (/) /\ B C_ A )
     -> A. g e. X_ x e. B C E. f e. X_ x e. A C g C_ f ) $=
      ( c0 wne wral wss wa cv cixp wrex wcel prl 3expia ralrimiv ) DIJABKZCBLZM
      GNZFNLFABDOPZGACDOZUAUBUCUEQUDABCDEFUCHRST $.
      $( [20-Nov-2011] $)
  $}

  ${
    $d A f g $.  $d B f g u $.  $d C f g $.  $d f g x $.  $d A x $.  $d B x $.
    prl2.1 $e |- A e. D $.
    $( Existence of a "prolongement" of a cartesian product.  Bourbaki E.II.34
       prop. 6.  (Contributed by FL, 20-Nov-2011.) $)
    prl2 $p |- ( ( A. x e. A C =/= (/) /\ B C_ A )
     -> A. g e. X_ x e. B C E. f e. X_ x e. A C g = ( f |` B ) ) $=
      ( vu wral wss wa cv wceq wrex wcel cfv wfn wi com12 c0 wne cres cixp prl1
      cdm elixp2a fndm wfun fnfun fvres eleq2 funssfv eqcomd 3exp syl6bi eqcoms
      w3a com13 com24 3imp eqeq2 imbi2d syl5ibr mpcom ancld ralrimiv ssid jctil
      3syl reximdvai ralimia syl wb ad2antlr fnssresb biimprcd syl5 imp eqfnfv3
      syl2anc rexbidva ralbidva mpbird ) DUAUBABJZCBKZLZGMZFMZCUCZNZFABDUDZOZGA
      CDUDZJCCKZIMZCPZWPWHQZWPWJQZNZLZICJZLZFWLOZGWNJZWGWHWIKZFWLOZGWNJXEABCDEF
      GHUEXGXDGWNWHWNPZWHCRZWHUFZCNZXGXDSACDWHUGZCWHUHXKXFXCFWLWIWLPZXKXFXCSZXM
      WIBRZWIUIZXKXNSABDWIUGZBWIUJXPXKXFXCXPXKXFURZXBWOXRXAICXRWQWTWQXRWTWSWPWI
      QZNZWQXRWTSZWPCWIUKWQYAXTXRWRXSNZSXRWQYBXPXKXFWQYBSZXKXPXFYCSXKWQXFXPYBWQ
      XFXPYBSSZSCXJCXJNWQWPXJPZYDCXJWPULXPXFYEYBXPXFYEYBXPXFYEURXSWRWPWIWHUMUNU
      OUSUPUQUTTVATXTWTYBXRWSXSWRVBVCVDVETVFVGCVHVIUOVJTVKVJVLVMWGWMXDGWNWGXHLZ
      WKXCFWLYFXMLXIWJCRZWKXCVNXHXIWGXMXLVOYFXMYGXMXOYFYGXQWFXOYGSWEXHXOYGWFBCW
      IVPVQVOVRVSICCWHWJVTWAWBWCWD $.
      $( [20-Nov-2011] $)
  $}


  ${
    $d A a f x y $.  $d A f u v x $.  $d B a f y $.  $d B f u v $.  $d C f y $.
    $d D a f y $.  $d I a f x y $.  $d I f u v x $.
    $( A projection is a mapping from a cartesian product onto one of its
       restriction.  Bourbaki E.II.33 prop. 5.  (Contributed by FL,
       20-Nov-2011.) $)
    prjmapcp2 $p |- ( ( I C_ A /\ A e. C /\
      ( A. x e. A B e. D /\ A. x e. A B =/= (/) ) )
      -> ( X_ x e. A B prj I ) : X_ x e. A B -onto-> X_ x e. I B ) $=
      ( vy vf va vu wss wcel wral wa cixp cv wceq wrex wi cvv wne w3a cproj cfv
      c0 co wf wfo prjmapcp 3adant3r sseq2 raleq anbi12d ixpeq1 rexeqdv ralbidv
      cres imbi12d pm3.2 adantl vex prl2 syl6com vtoclg com12 3imp simp2 simp3l
      cmpt jca adantr ixpexg syl ssexg 3adant3 isprj1 syl2anc fveq1d eqid resex
      reseq1 fvmpt eqtrd eqeq2d rexbidva mpbird dffo3 sylanbrc ) FBKZBDLZCELZAB
      MZCUEUAZABMZNZUBZABCOZAFCOZWQFUCUFZUGZGPZHPZWSUDZQZHWQRZGWRMZWQWRWSUHWIWJ
      WLWTWNABCDEFUIUJWPXFXAXBFUQZQZHWQRZGWRMZWIWJWOXJWJWIWOXJSZFIPZKZWKAXLMZWM
      AXLMZNZXHHAXLCOZRZGWRMZSZSWIXKSIBDXLBQZXMWIXTXKXLBFUKYAXPWOXSXJYAXNWLXOWN
      WKAXLBULWMAXLBULUMYAXRXIGWRYAXHHXQWQAXLBCUNUOUPURURXPXMXOXMNZXSXOXMYBSXNX
      OXMUSUTAXLFCTHGIVAVBVCVDVEVFWPXEXIGWRWPXDXHHWQWPXBWQLZNZXCXGXAYDXCXBJWQJP
      ZFUQZVIZUDZXGYDXBWSYGYDWQTLZFTLZWSYGQYDWJWLNZYIWPYKYCWPWJWLWIWJWOVGWIWJWL
      WNVHVJVKABCDEVLVMWPYJYCWIWJYJWOFBDVNVOVKWQJFTTVPVQVRYCYHXGQWPJXBYFXGWQYGY
      EXBFWAYGVSXBFHVAVTWBUTWCWDWEUPWFHGWQWRWSWGWH $.
      $( [31-May-2014] $) $( [20-Nov-2011] $)
  $}

  ${
    $d A f x z $.  $d B f y z $.  $d C f z $.  $d D y z $.  $d X z $.
    $d x y z $.
    dstr.1 $e |- ( y = ( f ` x ) -> C = D ) $.
    dstr.2 $e |- X = X_ x e. A B $.
    dstr.3 $e |- A e. E $.
    $( Distribution of union over intersection.  Bourbaki E.II.35 prop. 8.
       (Contributed by FL, 18-Jun-2011.) $)
    dstr $p |- U_ x e. A |^|_ y e. B C = |^|_ f e. X U_ x e. A D $=
      ( vz cv wcel wrex wral ralimi 3imtr4i wn ciin ciun eliun cixp cfv elixp2b
      wceq eleq2d rcla4v rexim 3syl eleq2s com12 ralrimiv ralbii sylibr cvv vex
      wi wb eliin ax-mp rexbii sylbi ralnex crab c0 wne rabn0 biimpri elexi wex
      ac9s wa wss ss2ixp ssrab2 a1i mprg sseli syl6eleqr notbid simprbi syl jca
      elrab eximi n0 df-rex sylib rexnal 3imtr3i sylbir con4i impbii eqriv ) MA
      CBDEUAZUBZGIACFUBZUAZMNZWROZXAWTOZXBXAWQOZACPZXCAXACWQUCZXAEOZBDQZACPZXAW
      SOZGIQZXEXCXIXAFOZACPZGIQZXKXIXMGIGNZIOZXIXMXIXMUSZXOACDUDZIXOXROANZXOUEZ
      DOZACQXHXLUSZACQXQACDXOUFYAYBACXGXLBXTDBNXTUGZEFXAJUHZUIRXHXLACUJUKKULUMU
      NXJXMGIAXACFUCUOZUPXDXHACXAUQOZXDXHUTMURZBXADEUQVAVBVCZYFXCXKUTYGGXAIWSUQ
      VAVBZSVDXKXEXCXBXNXIXKXEXIXNXITXHTZACQZXNTZXHACVEXGTZBDPZACQZXMTZGIPZYKYL
      YOXLTZACQZGIPZYQYOYMBDVFZVGVHZACQZYTYNUUBACUUBYNYMBDVIVJRUUCACUUAUDZVGVHZ
      YTACUUACHLVKVMXOUUDOZGVLXPYSVNZGVLUUEYTUUFUUGGUUFXPYSUUFXOXRIUUDXRXOUUADV
      OZUUDXRVOACACUUADVPUUHXSCOYMBDVQVRVSVTKWAUUFXTUUAOZACQYSACUUAXOUFUUIYRACU
      UIYAYRYMYRBXTDYCXGXLYDWBWFWCRWDWEWGGUUDWHYSGIWISVDWDYSYPGIXLACVEVCWJYNYJA
      CXGBDWKUOXMGIWKWLWMWNYEYHSYIXFSWOWP $.
      $( [18-Jun-2011] $)

  $}


