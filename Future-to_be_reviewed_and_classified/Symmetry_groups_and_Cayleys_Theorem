$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Symmetry groups and Cayley's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SymGrp $.

  $( Extend class notation to include the class of symmetry groups. $)
  csymg $a class SymGrp $.

  ${
    $d x y f g h $.
    $( Define the symmetry group on set ` x ` .  We represent the group as the
       set of 1-1-onto functions from ` x ` to itself under function
       composition.  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    df-symg $a |- SymGrp = ( x e. _V |-> { <. <. f , g >. , h >. |
      ( f : x -1-1-onto-> x /\ g : x -1-1-onto-> x /\ h = ( f o. g ) ) } ) $.
  $}

  ${
    $d A f g x y z $.  $d F f g x $.  $d G g $.  $d P f g y z $.
    elsymgrn.1 $e |- A e. _V $.
    elsymgrn.2 $e |- P = { x | x : A -1-1-onto-> A } $.
    $( Two ways of saying a function is a 1-1-onto mapping of A to itself.
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    elsymgrn $p |- ( F e. P <-> F : A -1-1-onto-> A ) $=
      ( wcel cvv wf1o elex wf f1of fex mpan2 syl cv f1oeq1 elab2g pm5.21nii ) D
      CGDHGZBBDIZDCJUABBDKZTBBDLUBBHGTEBBHDMNOBBAPZIUAADCHBBUCDQFRS $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( The value of the symmetry group function at ` A ` .  (Contributed by
       Paul Chapman, 25-Feb-2008.) $)
    symgval $p |- ( SymGrp ` A ) = ( f e. P , g e. P |-> ( f o. g ) ) $=
      ( vy vz cvv wcel csymg cv wceq wf1o w3a copab2 wa f1oeq2 f1oeq3 cfv cmpt2
      elsymgrn syl6bbr 3anbi12d df-3an syl6bb oprabbidv df-mpt2 syl6eqr df-symg
      ccom bitrd cmap co ovex wf f1of elmap sylibr abssi ssexi eqeltri mpt2exga
      cab mp2an fvmpt ax-mp ) BJKBLUADECCDMZEMZULZUBZNFHBHMZVMVIOZVMVMVJOZIMVKN
      ZPZDEIQZVLJLVMBNZVRVICKZVJCKZRVPRZDEIQVLVSVQWBDEIVSVQVTWAVPPWBVSVNVTVOWAV
      PVSVNBBVIOZVTVSVNBVMVIOWCVMBVMVISVMBBVITUMABCVIFGUCUDVSVOBBVJOZWAVSVOBVMV
      JOWDVMBVMVJSVMBBVJTUMABCVJFGUCUDUEVTWAVPUFUGUHDEICCVKUIUJHDEIUKCJKZWEVLJK
      CBBAMZOZAVEZJGWHBBUNUOZBBUNUPWGAWIWGBBWFUQWFWIKBBWFURBBWFFFUSUTVAVBVCZWJD
      ECCVKJJVDVFVGVH $.
      $( [20-Nov-2013] $) $( [25-Feb-2008] $)

    $( The value of the group operation of the symmetry group on ` A ` .
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgov $p |- ( ( F e. P /\ G e. P ) ->
                       ( F ( SymGrp ` A ) G ) = ( F o. G ) ) $=
      ( vf vg wcel ccom cvv csymg cfv co wceq coexg cv coeq1 coeq2 symgval
      ovmpt2g mpd3an3 ) DCJECJDEKZLJDEBMNZOUDPDECCQHIDECCHRZIRZKUDUEDUGKLUFDUGS
      UGEDTABCHIFGUAUBUC $.
      $( [20-Nov-2013] $) $( [25-Feb-2008] $)

    $( The domain and codomain of the group operation of the symmetry group on
       ` A ` .  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgf $p |- ( SymGrp ` A ) : ( P X. P ) --> P $=
      ( vf vg cv ccom wcel wral cxp csymg cfv wf wf1o wa f1oco elsymgrn anbi12i
      3imtr4i rgen2 symgval fmpt2 mpbi ) FHZGHZIZCJZGCKFCKCCLCBMNZOUIFGCCBBUFPZ
      BBUGPZQBBUHPUFCJZUGCJZQUIBBBUFUGRUMUKUNULABCUFDESABCUGDESTABCUHDESUAUBFGC
      CUHCUJABCFGDEUCUDUE $.
      $( [20-Nov-2013] $) $( [25-Feb-2008] $)
  $}

  ${
    $d A f g h x $.  $d F f g h $.  $d F x $.  $d G f g h $.  $d P f g h $.
    $d f g h x y $.
    symggrpi.1 $e |- A e. _V $.
    $( The symmetry group on ` A ` is a group (inference version).
       (Contributed by Paul Chapman, 4-Jun-2008.) $)
    symggrpi $p |- ( SymGrp ` A ) e. GrpOp $=
      ( vf vg vh vx cv wf1o cvv wcel wceq wa ccom symgov f1oco elsymgrn anbi12i
      co 3imtr4i eqtrd cid cres csymg cfv ccnv cab equid biantru abbii f1oabexg
      mp2an eqid symgf w3a coass 3adant3 oveq1d sylan 3impa oveq2d sylan2 3impb
      3adant1 3eqtr4a f1oi mpbir mpan wf f1of fcoi2 syl sylbi mpancom f1ococnv1
      f1ocnv isgrpoi ) CDEUAAUBZAUCUDZCGZUEZAAFGZHZFUFZAIJZWDWCIJBBWAWAKZAAIIFW
      CWBWBWELFWEWBFUGUHUIUJUKFAWCBWCULZUMVSWCJZDGZWCJZEGZWCJZUNZVSWHMZWJMZVSWH
      WJMZMZVSWHVRRZWJVRRZVSWHWJVRRZVRRZVSWHWJUOWLWRWMWJVRRZWNWLWQWMWJVRWGWIWQW
      MKWKFAWCVSWHBWFNUPUQWGWIWKXAWNKZWGWILZWMWCJZWKXBAAVSHZAAWHHZLAAWMHXCXDAAA
      VSWHOWGXEWIXFFAWCVSBWFPZFAWCWHBWFPZQFAWCWMBWFPSFAWCWMWJBWFNURUSTWLWTVSWOV
      RRZWPWLWSWOVSVRWIWKWSWOKWGFAWCWHWJBWFNVCUTWGWIWKXIWPKZWIWKLZWGWOWCJZXJXFA
      AWJHZLAAWOHXKXLAAAWHWJOWIXFWKXMXHFAWCWJBWFPQFAWCWOBWFPSFAWCVSWOBWFNVAVBTV
      DVQWCJZAAVQHAVEFAWCVQBWFPVFZWGVQVSVRRZVQVSMZVSXNWGXPXQKXOFAWCVQVSBWFNVGWG
      XEXQVSKZXGXEAAVSVHXRAAVSVIAAVSVJVKVLTXEAAVTHWGVTWCJZAAVSVOXGFAWCVTBWFPSZW
      GVTVSVRRZVTVSMZVQXSWGYAYBKXTFAWCVTVSBWFNVMWGXEYBVQKXGAAVSVNVLTVP $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( The value of the identity element of the symmetry group on ` A `
       (Contributed by Paul Chapman, 25-Jul-2008.) $)
    symgidi $p |- ( Id ` ( SymGrp ` A ) ) = ( _I |` A ) $=
      ( vx cid cres csymg cfv cgi wceq co ccom cv wf1o cab wcel f1oi eqid mpbir
      mp2an ax-mp elsymgrn symgov wf f1of fcoi2 eqtri cgr wb symggrpi cxp symgf
      fdmi grporn grpoid eqcomi ) DAEZAFGZHGZUPURIZUPUPUQJZUPIZUTUPUPKZUPUPAACL
      MCNZOZVDUTVBIVDAAUPMZAPZCAVCUPBVCQZUARZVHCAVCUPUPBVGUBSAAUPUCZVBUPIVEVIVF
      AAUPUDTAAUPUETUFUQUGOVDUSVAUHABUIZVHUPURUQVCUQVCVJVCVCUJVCUQCAVCBVGUKULUM
      URQUNSRUO $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)
  $}

