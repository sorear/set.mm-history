$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Symmetry groups and Cayley's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SymGrp $.

  $( Extend class notation to include the class of symmetry groups. $)
  csymgrp $a class SymGrp $.

  ${
    $d x y f g h $.
    $( Define the symmetry group on set ` x ` .  We represent the group as the
       set of 1-1-onto functions from ` x ` to itself under function
       composition.  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    df-symgrp $a |- SymGrp = { <. x , y >. | y = { <. <. f , g >. , h >. |
      ( f : x -1-1-onto-> x /\ g : x -1-1-onto-> x /\ h = ( f o. g ) ) } } $.
  $}

  ${
    $d A f g h x $.  $d F f g h $.  $d F x $.  $d G f g h $.  $d P f g h $.
    $d f g h x y $.
    elsymgrn.1 $e |- A e. _V $.
    elsymgrn.2 $e |- P = { x | x : A -1-1-onto-> A } $.
    $( Two ways of saying a function is a 1-1-onto mapping of A to itself.
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    elsymgrn $p |- ( F e. P <-> F : A -1-1-onto-> A ) $=
      ( wcel cvv wf1o elisset wf f1of fex mpan2 syl cv f1oeq1 elab2g pm5.21nii
      ) DCGDHGZBBDIZDCJUABBDKZTBBDLUBBHGTEBBHDMNOBBAPZIUAADCHBBUCDQFRS $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( Two ways to express the symmetry-group operator class abstraction.
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgoprab $p |- { <. <. f , g >. , h >. | ( f : A -1-1-onto-> A /\
                      g : A -1-1-onto-> A /\ h = ( f o. g ) ) } =
      { <. <. f , g >. , h >. | ( ( f e. P /\ g e. P ) /\ h = ( f o. g ) ) } $=
      ( cv wcel wa ccom wceq copab2 wf1o w3a elsymgrn anbi12i anbi1i df-3an
      bitr4i oprabbii eqcomi ) DIZCJZEIZCJZKZFIUDUFLMZKZDEFNBBUDOZBBUFOZUIPZDEF
      NUJUMDEFUJUKULKZUIKUMUHUNUIUEUKUGULABCUDGHQABCUFGHQRSUKULUITUAUBUC $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( The value of the symmetry group function at ` A ` .  (Contributed by
       Paul Chapman, 25-Feb-2008.) $)
    symgval $p |- ( SymGrp ` A ) = { <. <. f , g >. , h >. |
                    ( ( f e. P /\ g e. P ) /\ h = ( f o. g ) ) } $=
      ( vy csymgrp cfv cv wf1o ccom wceq w3a copab2 copab wcel wa df-symgrp
      fveq1i cvv cab equid biantru abbii eqtri f1oabexg mp2an symgoprab
      oprabex2 f1oeq2 f1oeq3 bitrd 3anbi12d oprabbidv fvopab 3eqtri ) BJKBILALZ
      UTDLZMZUTUTELZMZFLVAVCNZOZPZDEFQZOAIRZKBBVAMZBBVCMZVFPZDEFQZVACSVCCSTVFTD
      EFQBJVIAIDEFUAUBAIBVHVMGDEFCCVEVMBUCSZVNCUCSGGUTUTOZBBUCUCACCBBUTMZAUDVPV
      OTZAUDHVPVQAVOVPAUEUFUGUHUIUJZVRABCDEFGHUKZULUTBOZVGVLDEFVTVBVJVDVKVFVTVB
      BUTVAMVJUTBUTVAUMUTBBVAUNUOVTVDBUTVCMVKUTBUTVCUMUTBBVCUNUOUPUQURVSUS $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( The value of the group operation of the symmetry group on ` A ` .
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgoprv $p |- ( ( F e. P /\ G e. P ) ->
                       ( F ( SymGrp ` A ) G ) = ( F o. G ) ) $=
      ( vf vg vh wcel wa csymgrp cfv co cv ccom wceq copab2 symgval opreqi cvv
      coexg coeq1 coeq2 eqid oprabval2g mpd3an3 syl5eq ) DCKZECKZLDEBMNZODEHPZC
      KIPZCKLJPUMUNQZRLHIJSZOZDEQZULUPDEABCHIJFGTUAUJUKURUBKUQURRDECCUCHIJDECCU
      OURUPDUNQUBUMDUNUDUNEDUEUPUFUGUHUI $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( The domain and codomain of the group operation of the symmetry group on
       ` A ` .  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgf $p |- ( SymGrp ` A ) : ( P X. P ) --> P $=
      ( vf vg vh cv ccom csymgrp cfv symgval wf1o wa wcel f1oco elsymgrn
      anbi12i 3imtr4i foprab ) FGHCCFIZGIZJZCBKLABCFGHDEMBBUBNZBBUCNZOBBUDNUBCP
      ZUCCPZOUDCPBBBUBUCQUGUEUHUFABCUBDERABCUCDERSABCUDDERTUA $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)
  $}

  ${
    $d A f g h x $.  $d F f g h $.  $d F x $.  $d G f g h $.  $d P f g h $.
    $d f g h x y $.
    symggrpi.1 $e |- A e. _V $.
    $( The symmetry group on ` A ` is a group (inference version).
       (Contributed by Paul Chapman, 4-Jun-2008.) $)
    symggrpi $p |- ( SymGrp ` A ) e. GrpOp $=
      ( vf vg vh vx cid cres csymgrp cfv cv ccnv wf1o cab cvv wcel wceq wa
      equid biantru abbii f1oabexg mp2an eqid symgf w3a ccom co coass symgoprv
      3adant3 opreq1d f1oco elsymgrn anbi12i 3imtr4i sylan 3impa eqtrd 3adant1
      opreq2d sylan2 3impb 3eqtr4a f1oi mpbir mpan wf f1of fcoi2 syl sylbi
      f1ocnv mpancom f1ococnv1 isgrpoi ) CDEGAHZAIJZCKZLZAAFKZMZFNZAOPZWDWCOPBB
      WAWAQZAAOOFWCWBWBWERFWEWBFSTUAUBUCFAWCBWCUDZUEVSWCPZDKZWCPZEKZWCPZUFZVSWH
      UGZWJUGZVSWHWJUGZUGZVSWHVRUHZWJVRUHZVSWHWJVRUHZVRUHZVSWHWJUIWLWRWMWJVRUHZ
      WNWLWQWMWJVRWGWIWQWMQWKFAWCVSWHBWFUJUKULWGWIWKXAWNQZWGWIRZWMWCPZWKXBAAVSM
      ZAAWHMZRAAWMMXCXDAAAVSWHUMWGXEWIXFFAWCVSBWFUNZFAWCWHBWFUNZUOFAWCWMBWFUNUP
      FAWCWMWJBWFUJUQURUSWLWTVSWOVRUHZWPWLWSWOVSVRWIWKWSWOQWGFAWCWHWJBWFUJUTVAW
      GWIWKXIWPQZWIWKRZWGWOWCPZXJXFAAWJMZRAAWOMXKXLAAAWHWJUMWIXFWKXMXHFAWCWJBWF
      UNUOFAWCWOBWFUNUPFAWCVSWOBWFUJVBVCUSVDVQWCPZAAVQMAVEFAWCVQBWFUNVFZWGVQVSV
      RUHZVQVSUGZVSXNWGXPXQQXOFAWCVQVSBWFUJVGWGXEXQVSQZXGXEAAVSVHXRAAVSVIAAVSVJ
      VKVLUSXEAAVTMWGVTWCPZAAVSVMXGFAWCVTBWFUNUPZWGVTVSVRUHZVTVSUGZVQXSWGYAYBQX
      TFAWCVTVSBWFUJVNWGXEYBVQQXGAAVSVOVLUSVP $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( The value of the identity element of the symmetry group on ` A `
       (Contributed by Paul Chapman, 25-Jul-2008.) $)
    symgidi $p |- ( Id ` ( SymGrp ` A ) ) = ( _I |` A ) $=
      ( vx cid cres csymgrp cfv cgi wceq co ccom cv wf1o cab wcel f1oi eqid
      elsymgrn mpbir symgoprv mp2an wf f1of ax-mp fcoi2 eqtri cgr wb symggrpi
      cxp symgf fdmi grprn grpoid eqcomi ) DAEZAFGZHGZUPURIZUPUPUQJZUPIZUTUPUPK
      ZUPUPAACLMCNZOZVDUTVBIVDAAUPMZAPZCAVCUPBVCQZRSZVHCAVCUPUPBVGTUAAAUPUBZVBU
      PIVEVIVFAAUPUCUDAAUPUEUDUFUQUGOVDUSVAUHABUIZVHUPURUQVCUQVCVJVCVCUJVCUQCAV
      CBVGUKULUMURQUNUASUO $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)
  $}

