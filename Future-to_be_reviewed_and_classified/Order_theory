$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Order theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TosetRel $.

  $( Extend class notation with the class of all totally ordered sets. $)
  ccha $a class TosetRel $.

  $( Define the class of all totally ordered sets.  (Contributed by FL,
     3-Nov-2009.) $)
  df-toset $a |- TosetRel = { r e. PosetRel |
                ( U. U. r X. U. U. r ) C_ ( r u. `' r ) } $.

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    $( A quantifier-free way of expressing the total order predicate. $)
    qfto $p |- ( ( A X. B ) C_ ( R u. `' R ) <->
                 A. x e. A A. y e. B ( x R y \/ y R x ) ) $=
      ( cv cop cxp wcel ccnv cun wi wal wa wbr wo wss wral visset opelxp brun
      df-br brcnv orbi2i 3bitr3i imbi12i 2albii wrel wb relxp ssrel ax-mp r2al
      3bitr4i ) AFZBFZGZCDHZIZUQEEJZKZIZLZBMAMZUOCIUPDINZUOUPEOZUPUOEOZPZLZBMAM
      URVAQZVHBDRACRVCVIABUSVEVBVHUOUPCDBSZTUOUPVAOVFUOUPUTOZPVBVHUOUPEUTUAUOUP
      VAUBVLVGVFUOUPEASVKUCUDUEUFUGURUHVJVDUICDUJABURVAUKULVHABCDUMUN $.
      $( [22-Nov-2013] $)
  $}

  ${
    $d R r x y $.  $d X r x y $.
    istoset.1 $e |- X = U. U. R $.
    $( The predicate is a toset.  (Contributed by FL, 3-Nov-2009.) $)
    istoset $p |- ( R e. TosetRel <->
        ( R e. PosetRel /\ ( X X. X ) C_ ( R u. `' R ) ) ) $=
      ( vr cv cuni cxp ccnv cun wss cps ccha wceq unieq unieqd syl6eqr xpeq12
      syl11anc id cnveq uneq12d sseq12d df-toset elrab2 ) DEZFZFZUGGZUEUEHZIZJB
      BGZAAHZIZJDAKLUEAMZUHUKUJUMUNUGBMZUOUHUKMUNUGAFZFBUNUFUPUEANOCPZUQUGBUGBQ
      RUNUEAUIULUNSUEATUAUBDUCUD $.
      $( [22-Nov-2013] $) $( [1-Nov-2009] $)

    $( The predicate is a toset.  (Contributed by FL, 3-Nov-2009.) $)
    istoset3 $p |- ( R e. TosetRel <->
        ( R e. PosetRel /\ A. x e. X A. y e. X ( x R y \/ y R x ) ) ) $=
      ( ccha wcel cps cxp ccnv cun wss wa cv wbr wo wral istoset qfto anbi2i
      bitri ) CFGCHGZDDICCJKLZMUBANZBNZCOUEUDCOPBDQADQZMCDERUCUFUBABDDCSTUA $.
      $( [22-Nov-2013] $) $( [1-Nov-2009] $)
  $}

