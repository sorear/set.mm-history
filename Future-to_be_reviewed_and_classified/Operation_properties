$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Operation properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Ass $.

  $( Extend class notation with a device to add associativity to internal
     operations. $)
  cass $a class Ass $.

  ${
    $d g x y z $.
    $( A device to add associativity to various sorts of internal operations.
       The definition is meaningful when ` g ` is a magma at least. $)
    df-ass $a |- Ass = { g | A. x e. dom dom g A. y e. dom dom g
    A. z e. dom dom g ( ( x g y ) g z ) = ( x g ( y g z ) ) } $.
  $}

  $c ExId $.

  $( Extend class notation with the class of all the internal operations with
     an identity element. $)
  cexid $a class ExId $.

  ${
    $d g x y $.
    $( A device to add an identity element to various sorts of internal
       operations. $)
    df-exid $a |- ExId = { g | E. x e. dom dom g A. y e. dom dom g
    ( ( x g y ) = y /\ ( y g x ) = y ) } $.
  $}

  ${
    $d G g x y z $.  $d X g x y z $.
    isass.1 $e |- X = dom dom G $.
    $( The predicate "is an associative operation".  (Contributed by FL,
       1-Nov-2009.) $)
    isass $p |- ( G e. A -> ( G e. Ass <-> A. x e. X A. y e. X A. z e. X
      ( ( x G y ) G z ) = ( x G ( y G z ) ) ) ) $=
      ( vg cv co wceq cdm wral wcel w3a wi wal eleq2d opreq raleqdv dmeqd eqtrd
      cass dmeq 3anbi123d opreq1d opreq2d eqeq12d imbi12d albidv 2albidv eqcomi
      r3al 3bitr4g a1i ralbidv 2ralbidv 3bitrd bitrd df-ass elab2g ) AIZBIZHIZJ
      ZCIZVDJZVBVCVFVDJZVDJZKZCVDLZLZMBVLMAVLMZVBVCEJZVFEJZVBVCVFEJZEJZKZCFMZBF
      MAFMZHEUCDVDEKZVMVRCELZLZMZBWCMZAWCMZVTWAVBVLNZVCVLNZVFVLNZOZVJPZCQZBQAQV
      BWCNZVCWCNZVFWCNZOZVRPZCQZBQAQVMWFWAWLWRABWAWKWQCWAWJWPVJVRWAWGWMWHWNWIWO
      WAVLWCVBWAVKWBVDEUDUAZRWAVLWCVCWSRWAVLWCVFWSRUEWAVGVOVIVQWAVGVNVFVDJVOWAV
      EVNVFVDVBVCVDESUFVNVFVDESUBWAVIVBVPVDJVQWAVHVPVBVDVCVFVDESUGVBVPVDESUBUHU
      IUJUKVJABCVLVLVLUMVRABCWCWCWCUMUNWAWFWEAFMWDBFMZAFMVTWAWEAWCFWCFKWAFWCGUL
      UOZTWAWEWTAFWAWDBWCFXATUPWAWDVSABFFWAVRCWCFXATUQURUSABCHUTVA $.
      $( [1-Nov-2009] $)
  $}

  ${
    $d G g x y $.  $d X g x y $.
    isexid.1 $e |- X = dom dom G $.
    $( The predicate ` G ` has a left and right identity element.  (Contributed
       by FL, 2-Nov-2009.) $)
    isexid $p |- ( G e. A -> ( G e. ExId <-> E. x e. X A. y e. X
      ( ( x G y ) = y /\ ( y G x ) = y ) ) ) $=
      ( vg cv co wceq wa cdm wral wrex cexid dmeq dmeqd syl6eqr opreq eqeq1d
      anbi12d raleqbidv rexeqbidv df-exid elab2g ) AHZBHZGHZIZUGJZUGUFUHIZUGJZK
      ZBUHLZLZMZAUONUFUGDIZUGJZUGUFDIZUGJZKZBEMZAENGDOCUHDJZUPVBAUOEVCUODLZLEVC
      UNVDUHDPQFRZVCUMVABUOEVEVCUJURULUTVCUIUQUGUFUGUHDSTVCUKUSUGUGUFUHDSTUAUBU
      CABGUDUE $.
      $( [22-Dec-2013] $) $( [2-Nov-2009] $)
  $}

