$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Initial and final topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c subSp $. $( Function returning a subspace topology. $)

  $( Extend class notation with the function returning a subspace topology. $)
  csubsp $a class subSp $.


  ${
    $d x y z u v $.
    $( Function returning the subspace topology induced by the topology ` y `
       and the set ` x ` . $)
    df-subsp $a |- subSp = { <. <. x , y >. , z >. |
       ( y e. Top /\ z = { u | E. v e. y u = ( v i^i x ) } ) } $.
  $}

  ${
    $d A u v x y z $.  $d J u v x y z $.
    subsp.1 $e |- J e. Top $.
    subsp.2 $e |- A e. _V $.
    $( The subspace topology induced by the topology ` J ` on the set ` A ` .
       (Contributed by FL, 4-Jun-2007.) $)
    subsp $p |- ( subSp ` <. A , J >. ) = { u | E. v e. J u = ( v i^i A ) } $=
      ( vx vy vz csubsp co cv cvv wcel ctop wa cin wceq wrex cab copab2 cop
      cfv df-subsp wb visset ibar anbi1d ax-mp oprabbii eqtri opreqi df-opr
      wss id inss2 a1i eqsstrd pm4.71ri rexbii r19.42v bitri abbii abssexg
      eqeltri ineq2 eqeq2d rexbidv abbidv rexeq eqid oprabval2 mp2an 3eqtr3i )
      CDJKCDGLZMNZHLZONZPZILBLZALZVOQZRZAVQSZBTZRZPZGHIUAZKZCDUBJUCVTWACQZRZADS
      ZBTZJWHCDJVRWFPZGHIUAWHGHIABUDWNWGGHIVPWNWGUEGUFVPVRVSWFVPVRUGUHUIUJUKULC
      DJUMCMNZDONWIWMRFEGHICDMOWEWMWHWKAVQSZBTWMVTCUNZWLPZBTZMWLWRBWLWQWKPZADSW
      RWKWTADWKWQWKVTWJCWKUOWJCUNWKWACUPUQURUSUTWQWKADVAVBVCWOWSMNFWLBCMVDUIVEV
      OCRZWDWPBXAWCWKAVQXAWBWJVTVOCWAVFVGVHVIVQDRWPWLBWKAVQDVJVIWHVKVLVMVN $.
      $( [1-Dec-2010] $) $( [29-May-2007] $)
  $}

  ${
    $d A u v $.  $d B u v $.  $d J u v $.
    issubsp.1 $e |- B e. _V $.
    issubsp.2 $e |- J e. Top $.
    $( The predicate "is a subspace topology".  (Contributed by FL,
       22-Dec-2008.) $)
    issubsp $p |- ( A e. C -> ( A e. ( subSp ` <. B , J >. )
      <-> E. v e. J A = ( v i^i B ) ) ) $=
      ( vu wcel cop csubsp cfv cv cin wceq wrex cab subsp a1i eleq2d eqeq1
      rexbidv elabg bitrd ) BDIZBCEJKLZIBHMZAMCNZOZAEPZHQZIBUHOZAEPZUEUFUKBUFUK
      OUEAHCEGFRSTUJUMHBDUGBOUIULAEUGBUHUAUBUCUD $.
      $( [1-Dec-2010] $) $( [22-Dec-2008] $)
  $}

  ${
    $d A v $.  $d B v $.  $d J v $.
    issubsplem1.1 $e |- J e. Top $.
    $( The predicate "is a subspace topology".  (Contributed by FL,
       28-Jan-2009.) $)
    issubsplem1 $p |- ( ( A e. C /\ B e. _V ) -> ( A e. ( subSp ` <. B , J >. )
      <-> E. v e. J A = ( v i^i B ) ) ) $=
      ( cvv wcel cop csubsp cfv cv cin wceq wrex wb wi c0 cif opeq1 fveq2d
      eleq2d ineq2 eqeq2d rexbidv bibi12d imbi2d 0ex elimel issubsp dedth
      impcom ) CGHZBDHZBCEIZJKZHZBALZCMZNZAEOZPZUMUNVBQUNBUMCRSZEIZJKZHZBURVCMZ
      NZAEOZPZQCRCVCNZVBVJUNVKUQVFVAVIVKUPVEBVKUOVDJCVCETUAUBVKUTVHAEVKUSVGBCVC
      URUCUDUEUFUGABVCDECRGUHUIFUJUKUL $.
      $( [1-Dec-2010] $) $( [5-Jan-2009] $)
  $}

  ${
    $d A v $.  $d B v $.  $d J v $.
    $( The predicate "is an open set of a subspace topology".  (Contributed by
       FL, 28-Jan-2009.) $)
    issubspt $p |- ( ( J e. Top /\ A e. C /\ B e. _V ) ->
      ( A e. ( subSp ` <. B , J >. ) <-> E. v e. J A = ( v i^i B ) ) ) $=
      ( ctop wcel cvv cop csubsp cfv cv cin wceq wrex wb wi c0 csn cif opeq2
      fveq2d eleq2d rexeq bibi12d imbi2d sn0top elimel issubsplem1 ex dedth
      3imp ) EFGZBDGZCHGZBCEIZJKZGZBALCMNZAEOZPZUMUNUOVAQZQUNUOBCUMERSZTZIZJKZG
      ZUSAVDOZPZQZQEVCEVDNZVBVJUNVKVAVIUOVKURVGUTVHVKUQVFBVKUPVEJEVDCUAUBUCUSAE
      VDUDUEUFUFUNUOVIABCDVDEVCFUGUHUIUJUKUL $.
      $( [1-Dec-2010] $) $( [5-Jan-2009] $)
  $}

  ${
    $d o A $.  $d o B $.  $d o J $.  $d o S $.  $d o X $.
    elsubsp.1 $e |- X = U. J $.
    $( Result used many times.  (Contributed by Jeff Hankins, 11-Jul-2009.) $)
    elsubsp $p |- ( ( ( J e. Top /\ S C_ X ) /\ ( A e. J /\ B = ( A i^i S ) ) )
                  -> B e. ( subSp ` <. S , J >. ) ) $=
      ( vo ctop wcel wss wa cin wceq cop csubsp cfv cv wrex ineq1 eqeq2d
      rcla4ev adantl cvv wb simpll inss1 sseq1 mpbiri ad2antll simprl ssexg
      syl11anc cuni ancoms uniexg sylan sseq2i sylan2b adantr issubspt
      syl111anc mpbird ) DHIZCEJZKZADIZBACLZMZKZKZBCDNOPIZBGQZCLZMZGDRZVIVOVEVN
      VHGADVLAMVMVGBVLACSTUAUBVJVCBUCIZCUCIZVKVOUDVCVDVIUEVJBAJZVFVPVHVRVEVFVHV
      RVGAJACUFBVGAUGUHUIVEVFVHUJBADUKULVEVQVIVCCDUMZJZVQVDVSUCIZVTVQVCVTWAVQCV
      SUCUKUNDHUOUPEVSCFUQURUSGBCUCDUTVAVB $.
      $( [1-Dec-2010] $) $( [11-Jul-2009] $)
  $}

  ${
    $d o t J $.  $d o t X $.
    subspid.1 $e |- X = U. J $.
    $( The subspace topology of the base set is the original topology.
       (Contributed by Jeff Hankins, 9-Jul-2009.) $)
    subspid $p |- ( J e. Top -> ( subSp ` <. X , J >. ) = J ) $=
      ( vo vt ctop wcel cop csubsp cfv cv cin wceq wrex cvv wb cuni uniexg
      syl5eqel visset issubspt mp3an2 mpdan wa eleq1 topopn adantr inopn
      mpd3an3 syl5cbir r19.23adva simpr wss eltopss df-ss sylib eqcomd ineq1
      eqeq2d rcla4ev syl11anc ex impbid bitrd eqrdv ) AFGZDBAHIJZAVFDKZVGGZVHEK
      ZBLZMZEANZVHAGZVFBOGZVIVMPZVFAQOBAFRCSVFVHOGVOVPDTEVHBOAUAUBUCVFVMVNVFVLV
      NEAVLVNVKAGZVFVJAGZUDVHVKAUEVFVRBAGZVQVFVSVRABCUFUGVJBAUHUIUJUKVFVNVMVFVN
      UDZVNVHVHBLZMZVMVFVNULVTWAVHVTVHBUMWAVHMVHABCUNVHBUOUPUQVLWBEVHAVJVHMVKWA
      VHVJVHBURUSUTVAVBVCVDVE $.
      $( [1-Dec-2010] $) $( [9-Jul-2009] $)
  $}

  ${
    $d A x y $.  $d J x y $.
    stoiglem.1 $e |- J e. Top $.
    stoiglem.2 $e |- A C_ U. J $.
    $( The underlying set of a subspace topology.  (Contributed by FL,
       28-Jan-2009.) $)
    stoiglem $p |- <. A , ( subSp ` <. A , J >. ) >. e. TopSp $=
      ( vy vx cop csubsp cfv ctps wcel ctop cuni wceq istps cv cin wrex cab
      cvv uniexg ax-mp ssexi subsp subtop eqeltri wss dfss mpbi incom inuni
      3eqtri unieqi eqtr4i mpbir2an ) AABGHIZGJKUPLKAUPMZNAUPOUPEPFPAQNFBRESZLF
      EABCABMZBLKZUSTKCBLUAUBDUCUDZUTURLKCEFBAUEUBUFAURMZUQAAUSQZUSAQVBAUSUGAVC
      NDAUSUHUIAUSUJEFBAUKULUPURVAUMUNUO $.
      $( [1-Dec-2010] $) $( [5-Jan-2009] $)
  $}

  $( The topological space built with a subspace topology.  (Contributed by FL,
     28-Jan-2009.) $)
  stoig $p |- ( ( J e. Top /\ A C_ U. J ) ->
   <. A , ( subSp ` <. A , J >. ) >. e. TopSp ) $=
    ( ctop wcel cuni wss wa cop csubsp cfv ctps c0 csn cif wceq opeq2 fveq2d
    opeq2d eleq1d id opeq1 opeq12d iftrue simpl eqeltrd wn iffalse sn0top
    syl6eqel pm2.61i sseq1 unieq sseq2d sylan9bb simpr syl5cbir mp2and 0ss
    mpbiri syl11anc stoiglem dedth2v ) BCDZABEZFZGZAABHZIJZHZKDAAVFBLMZNZHZIJZH
    ZKDVFALNZVOVKHZIJZHZKDBAVJLBVKOZVIVNKVSVHVMAVSVGVLIBVKAPQRSAVOOZVNVRKVTAVOV
    MVQVTTVTVLVPIAVOVKUAQUBSVOVKVFVKCDVFVKBCVFBVJUCZVCVEUDUEVFUFZVKVJCVFBVJUGZU
    HUIUJVFVOVKEZFZVFVOAOZVKBOZWEVFALUCWAWFWGGWEVEVFWFWEAWDFWGVEVOAWDUKWGWDVDAV
    KBULUMUNVCVEUOUPUQWBVOLOZVKVJOZWEVFALUGWCWHWIGWELVJEZFZWJURWHWELWDFWIWKVOLW
    DUKWIWDWJLVKVJULUMUNUSUTUJVAVB $.
    $( [1-Dec-2010] $) $( [5-Jan-2009] $)

  $( The underlying set of a subspace topology.  (Contributed by FL,
     28-Jan-2009.) $)
  stoig2 $p |- ( ( J e. Top /\ A C_ U. J ) ->
   U. ( subSp ` <. A , J >. ) = A ) $=
    ( ctop wcel cuni wss wa cop csubsp cfv ctps wceq stoig istps id eqcoms
    adantl sylbi syl ) BCDABEFGAABHIJZHKDZTEZALZABMUATCDZAUBLZGUCATNUEUCUDUCUBA
    UCOPQRS $.
    $( [1-Dec-2010] $) $( [5-Jan-2009] $)

  $( A subspace topology is a topology.  (Contributed by FL, 28-Jan-2009.) $)
  stoig3 $p |- ( ( J e. Top /\ A C_ U. J ) ->
    ( subSp ` <. A , J >. ) e. Top ) $=
    ( ctop wcel cuni wss wa cop csubsp cfv ctps stoig wceq istps simplbi syl )
    BCDABEFGAABHIJZHKDZQCDZABLRSAQEMAQNOP $.
    $( [1-Dec-2010] $) $( [5-Jan-2009] $)

  ${
    $d o x A $.  $d o x J $.  $d o x S $.  $d o x X $.
    subcld.1 $e |- X = U. J $.
    $( A closed set of a subspace topology is a closed set of the original
       topology intersected with the subset.  (Contributed by FL,
       11-Jul-2009.) $)
    subcld $p |- ( ( J e. Top /\ S C_ X ) -> ( A e. ( Clsd ` ( subSp ` <. S , J
    >. ) ) <-> E. x e. ( Clsd ` J ) A = ( x i^i S ) ) ) $=
      ( vo ctop wcel wss wa cop csubsp cfv ccld cuni cdif cv cin wceq wrex wb
      stoig3 sseq2i sylan2b eqid iscld syl stoig2 sseq2d difeq1d eleq1d
      anbi12d cvv simpl ssexg ssdifss sylan ancoms uniexg biimpi syl2an
      issubspt syl111anc anbi2d opncld adantlr adantr df-ss incom syl5eq
      ad2antlr ad2antrr difeq2 cun c0 difindi difid uneq2i un0 3eqtri syl6eq
      adantl dfss4 3eqtr2rd difeq1i indif2 3eqtr2i ineq1 eqeq2d rcla4ev
      syl11anc ex r19.23adva expimpd sylbid wi inss2 sseq1 mpbiri a1i simpll
      cldopn syl5req 3eqtrri 3eqtrd elsubsp syl12anc jcad impbid 3bitrd ) DHIZC
      EJZKZBCDLMNZONIZBXOPZJZXQBQZXOIZKZBCJZCBQZXOIZKZBARZCSZTZADONZUAZXNXOHIZX
      PYAUBXLCDPZJZYKXMCDUCEYLCFUDZUEBXOXQXQUFUGUHXLYMYAYEUBXMXLYMKZXRYBXTYDYOX
      QCBCDUIZUJYOXSYCXOYOXQCBYPUKULUMYNUEXNYEYJXNYEYBYCGRZCSZTZGDUAZKYJXNYDYTY
      BXNXLYCUNIZCUNIZYDYTUBXLXMUOYLUNIZYMUUAXLXMYMUUCUUAYCYLJUUCUUAYMYCYLUNUPC
      YLBUQURUSDHUTZXMYMYNVAZVBUUCYMUUBXLXMYMUUCUUBCYLUNUPUSUUDUUEVBGYCCUNDVCVD
      VEXNYBYTYJXNYBKZYSYJGDUUFYQDIZKZYSYJUUHYSKZEYQQZYIIZBUUJCSZTZYJUUHUUKYSXN
      UUGUUKYBXLUUGUUKXMYQDEFVFVGVGVHUUIBECSZYQQZUULUUIUUOCYQQZCYCQZBUUIUUNCYQU
      UFUUNCTZUUGYSXMUURXLYBXMCESZCUUNXMUUSCTCEVIVAZECVJZVKVLVMUKYSUUQUUPTUUHYS
      UUQCYRQZUUPYCYRCVNUVBUUPCCQZVOUUPVPVOUUPCYQCVQUVCVPUUPCVRZVSUUPVTWAWBWCUU
      FUUQBTZUUGYSYBUVEXNYBUVEBCWDVAWCVMWEUUOUUSYQQCUUJSUULUUNUUSYQUVAWFCEYQWGC
      UUJVJWHWBYHUUMAUUJYIYFUUJTYGUULBYFUUJCWIWJWKWLWMWNWOWPXNYHYEAYIXNYFYIIZKZ
      YHYBYDYHYBWQUVGYHYBYGCJYFCWRBYGCWSWTXAUVGYHYDUVGYHKZXNEYFQZDIZYCUVICSZTYD
      XNUVFYHXBUVGUVJYHXLUVFUVJXMYFDEFXCVGVHUVHYCCYGQZCYFQZUVKYHYCUVLTUVGBYGCVN
      WCUVLUVMTUVHUVLUVMUVCVOUVMVPVOUVMCYFCVQUVCVPUVMUVDVSUVMVTWAXAXNUVMUVKTZUV
      FYHXMUVNXLXMUVMUUNYFQZUVKXMCUUNYFXMUUSCUUNUUTUVAXDUKUVKCUVISUUSYFQUVOUVIC
      VJCEYFWGUUSUUNYFCEVJWFXEWBWCVMXFUVIYCCDEFXGXHWMXIWNXJXK $.
      $( [1-Dec-2010] $) $( [11-Jul-2009] $)
  $}

  ${
    $d o r s t x y z A $.  $d o r s t x y z C $.  $d o r s t x y z D $.
    $d o r s t x y z J $.  $d o r s t x y z K $.  $d o r s t x y z X $.
    $d o r s t x y z Y $.
    subtopmet.1 $e |- D = ( C |` ( Y X. Y ) ) $.
    subtopmet.2 $e |- X = dom dom C $.
    subtopmet.j $e |- J = ( Open ` C ) $.
    subtopmet.k $e |- K = ( Open ` D ) $.
    ${
      subtopmet.a $e |- A e. _V $.
      $( Lemma for ~ subtopmet .  (Contributed by Jeff Hankins,
         21-Aug-2009.) $)
      subtopmetlem $p |- ( ( C e. Met /\ Y C_ X ) -> ( A e. ( subSp ` <. Y , J
      >. ) -> A e. K ) ) $=
        ( vy vx vo vr vz cme wcel wss wa cv cin wceq wrex cdm cbl cfv crn wral
        cop csubsp w3a inss2 sseq1 mpbiri 3ad2ant3 basmetres 3ad2ant1 sseqtrd
        cc0 clt wbr co cr simpl1l simp2 adantr ssel2 inss1 sylan opni2
        syl111anc wi cxp cres metres syl5eqel 3adant3 syl3an1 eleqtrd simp3
        simprl eqid blelrn syl22anc blcntr simplrr wfun wfn wf metf ffun syl
        ad2antrr df-fn resss eqsstri funss ax-mp simp11r xpss12 syl11anc fdm
        sseqtr4d df-ss sylib dmeqi dmres eqtri syl5eq sylanbrc a1i eqimss2
        sseld imp adantrr oprssoprv syl32anc simprr eqbrtrd wb simp11l sseldd
        dmss sseli syl6eleqr ad2antrl simpll3 simplrl elbl2 mpbird ex dmxpid
        syl6ss syl5ss adantrd jcad elin syl6ibr elbl simpl13 eleq2d 3imtr4d
        ssrdv eleq2 anbi12d rcla4ev syl12anc 3expia r19.23adv mpd r19.21aiva
        jca 3exp ctop cvv opntop ssexg dmexg sylan2 sseq2i sylanb ancoms
        issubspt isopn ) BRSZGFTZUAZAMUBZGUCZUDZMDUEZACUFZUFZTZNUBZOUBZSZUVRATZ
        UAZOCUGUHZUIZUEZNAUJZUAZAGDUKULUHSZAESZUVIUVLUWFMDUVIUVJDSZUVLUWFUVIUWI
        UVLUMZUVPUWEUWJAGUVOUVLUVIAGTZUWIUVLUWKUVKGTUVJGUNAUVKGUOUPUQZUVIUWIGUV
        OUDZUVLBCFGHIURUSZUTUWJUWDNAUWJUVQASZUAZVAPUBZVBVCZUVQUWQBUGUHVDZUVJTZU
        AZPVEUEZUWDUWPUVGUWIUVQUVJSZUXBUVGUVHUWIUVLUWOVFUWJUWIUWOUVIUWIUVLVGVHA
        UVJTZUWOUXCUWJAUVJUVQVIUVLUVIUXDUWIUVLUXDUVKUVJTUVJGVJAUVKUVJUOUPUQVKPU
        VJBUVQDJVLVMUWPUXAUWDPVEUWJUWOUWQVESZUXAUWDVNUWJUWOUXEUMZUXAUWDUXFUXAUA
        ZUVQUWQUWBVDZUWCSZUVQUXHSZUXHATZUWDUXGCRSZUVQUVOSZUXEUWRUXIUXFUXLUXAUWJ
        UWOUXLUXEUVIUWIUXLUVLUVGUXLUVHUVGBGGVOZVPZRCBGVQZHVRVHUSUSVHZUXGUVQGUVO
        UXFUVQGSZUXAUWKUWOUXEUXRUWJUWKUWOUXRUXEAGUVQVIVSUWLVTZVHUXFUWMUXAUWJUWO
        UWMUXEUWNUSVHZWAZUXFUXEUXAUWJUWOUXEWBVHZUXFUWRUWTWCZCUVQUWQUVOUVOWDZWEW
        FUXGUXLUXMUXEUWRUXJUXQUYAUYBUYCCUVQUWQUVOUYDWGWFUXGQUXHAUXGQUBZUVOSZUVQ
        UYECVDZUWQVBVCZUAZUYEUVKSZUYEUXHSZUYEASUXGUYIUYEUVJSZUYEGSZUAUYJUXGUYIU
        YLUYMUXGUYIUYLUXGUYIUAZUWSUVJUYEUXFUWRUWTUYIWHUYNUYEUWSSZUVQUYEBVDZUWQV
        BVCZUYNUYPUYGUWQVBUYNBWIZCUXNWJZCBTZUXRUYMUYPUYGUDUXFUYRUXAUYIUWJUWOUYR
        UXEUVIUWIUYRUVLUVGUYRUVHUVGFFVOZVEBWKZUYRBFIWLZVUAVEBWMWNVHUSUSWOZUYSCW
        IZUVNUXNUDUYNCUXNWPUYNUYRVUEVUDUYTUYRVUEVNCUXOBHBUXNWQWRZCBWSWTWNUYNUXN
        BUFZUCZUXNUVNUYNUXNVUGTVUHUXNUDUYNUXNVUAVUGUYNUVHUVHUXNVUATUXFUVHUXAUYI
        UVGUVHUWIUVLUWOUXEXAWOZVUIGFGFXBXCUXFVUGVUAUDZUXAUYIUWJUWOVUJUXEUVIUWIV
        UJUVLUVGVUJUVHUVGVUBVUJVUCVUAVEBXDWNVHUSUSWOXEUXNVUGXFXGUVNUXOUFVUHCUXO
        HXHBUXNXIXJZXKXLUYTUYNVUFXMUXFUXRUXAUYIUXSWOZUXGUYFUYMUYHUXGUYFUYMUXGUV
        OGUYEUXGUWMUVOGTUXTUVOGXNWNXOXPXQUVQUYEGGBCXRXSUXGUYFUYHXTYAUYNUVGUVQFS
        UYEFSZUXEUWRUYOUYQYBUXFUVGUXAUYIUVGUVHUWIUVLUWOUXEYCWOUYNGFUVQVUIVULYDU
        YFVUMUXGUYHUYFUYEVUGUFZFUVOVUNUYEUVNVUGTZUVOVUNTUYTVUOVUFCBYEWTUVNVUGYE
        WTYFIYGYHUWJUWOUXEUXAUYIYIUXFUWRUWTUYIYJUYEBUVQUWQFIYKXSYLYDYMUXGUYFUYM
        UYHUXGUVOGUYEUXGVUHUFZGUVOUXGVUPUXNUFZGVUPVUQTZUXGVUHUXNTVURUXNVUGVJVUH
        UXNYEWTXMGYNYOUVNVUHVUKXHYPXOYQYRUYEUVJGYSYTUXGUXLUXMUXEUWRUYKUYIYBUXQU
        YAUYBUYCUYECUVQUWQUVOUYDUUAWFUXGAUVKUYEUVIUWIUVLUWOUXEUXAUUBUUCUUDUUEUW
        AUXJUXKUAOUXHUWCUVRUXHUDUVSUXJUVTUXKUVRUXHUVQUUFUVRUXHAUOUUGUUHUUIYMUUJ
        UUKUULUUMUUNUUOUUKUVIDUUPSZAUUQSZGUUQSZUWGUVMYBUVGVUSUVHBDJUURVHVUTUVIL
        XMUVHUVGVVAGVUNTZUVGVVAUVHVVBVUNUUQSZVVAUVGGVUNUUQUUSUVGVUGUUQSVVCBRUUT
        VUGUUQUUTWNUVAFVUNGIUVBUVCUVDMAGUUQDUVEVMUVIUXLUWHUWFYBUVIUXORCUVGUXORS
        UVHUXPVHHVRNOACEUVOUYDKUVFWNUUD $.
        $( [1-Dec-2010] $) $( [21-Aug-2009] $)
    $}

    $( Two alternate formulations of a subspace topology of a metric space
       topology.  (Contributed by Jeff Hankins, 21-Aug-2009.) $)
    subtopmet $p |- ( ( C e. Met /\ Y C_ X ) -> ( subSp ` <. Y , J >. ) = K )
        $=
      ( vt vy vr vx vz vs cme wcel wss wa cop csubsp cfv cv visset
      subtopmetlem ctop cuni cbl co wceq crp wrex cab cin opntop ad2antrr
      simpr uniopn2 adantr sseqtr4d w3a cr cc0 clt wbr simp1ll ssel2 simplr
      sylan adantrr 3adant3 rpre adantl 3ad2ant2 rpgt0 blopn syl22anc wb eleq1
      3ad2ant3 mpbird 3exp r19.23advv abssdv uniopn syl11anc wex cxp cres
      metres syl5eqel simprl simprr opni2 syl111anc simplll cdm eqid opnss
      ad2ant2r basmetres sseldd simprrl blcntr elrp biimpri simprrr eqidd
      opreq1 sseq1d eqeq2d anbi12d opreq2 rcla42ev syl112anc oprex eleq2 eqeq1
      anbi2d 2rexbidv cla4ev exp32 r19.23adv mpd expr eluniab syl6ibr sseld
      jcad elin wi simpll ad2antlr eleqtrd ad2antll elbl adantlr wfun wfn wf
      metf ffun syl ffn 3syl resss eqsstri a1i oprssoprv syl32anc simpl
      adantll rpregt0 elbl2 syl31anc mpbid eqbrtrrd mpbir2and exp53 com14 imp
      com12 19.23adv imp3a biimpi sylani syl5ib impbid eqrdv elsubsp ex ) AQRZF
      ESZTZKFCUAUBUCZDUVOKUDZUVPRZUVQDRZUVQABCDEFGHIJKUEUFUVOUVSUVRUVOUVSTZCUGR
      ZFCUHZSZLUDZMUDZBUIUCZUJZUVQSZNUDZUWDUWEAUIUCZUJZUKZTZMULUMLFUMZNUNZUHZCR
      ZUVQUWPFUOZUKUVRUVMUWAUVNUVSACIUPUQZUVOUWCUVSUVOFEUWBUVMUVNURUVMUWBEUKUVN
      ACEHIUSUTVAUTUVTUWAUWOCSUWQUWSUVTUWNNCUVTUWMUWICRZLMFULUVTUWDFRZUWEULRZTZ
      UWMUWTUVTUXCUWMVBZUWTUWKCRZUXDUVMUWDERZUWEVCRZVDUWEVEVFZUXEUVMUVNUVSUXCUW
      MVGUVTUXCUXFUWMUVTUXAUXFUXBUVNUXAUXFUVTFEUWDVHZUVMUVNUVSVIVJVKVLUXCUVTUXG
      UWMUXBUXGUXAUWEVMZVNVOUXCUVTUXHUWMUXBUXHUXAUWEVPZVNVOAUWDUWECEHIVQVRUWMUV
      TUWTUXEVSZUXCUWLUXLUWHUWIUWKCVTVNWAWBWCWDWEUWOCWFWGUVTOUVQUWRUVTOUDZUVQRZ
      UXMUWRRZUVTUXNUXMUWPRZUXMFRZTZUXOUVTUXNUXPUXQUVTUXNUXMUWIRZUWNTZNWHZUXPUV
      OUVSUXNUYAUVOUVSUXNTZTZVDPUDZVEVFZUXMUYDUWFUJZUVQSZTZPVCUMZUYAUYCBQRZUVSU
      XNUYIUVMUYJUVNUYBUVMAFFWIZWJZQBAFWKGWLZUQUVOUVSUXNWMUVOUVSUXNWNZPUVQBUXMD
      JWOWPUYCUYHUYAPVCUYCUYDVCRZUYHUYAUYCUYOUYHTZTZUXMUXMUYDUWJUJZRZUWHUYRUWKU
      KZTZMULUMLFUMZUYAUYQUVMUXMERZUYOUYEUYSUVMUVNUYBUYPWQUYCVUCUYPUYCFEUXMUVMU
      VNUYBVIUYCUVQFUXMUYCUVQBWRWRZFUVMUVSUVQVUDSZUVNUXNUYJUVSVUEUVMUVQBDVUDVUD
      WSZJWTZUYMVJXAUVOFVUDUKZUYBABEFGHXBZUTVAUYNXCZXCUTUYCUYOUYHWMUYCUYOUYEUYG
      XDAUXMUYDEHXEVRUYQUXQUYDULRZUYGUYRUYRUKZVUBUYCUXQUYPVUJUTUYPVUKUYCUYOUYEV
      UKUYGVUKUYOUYETUYDXFXGVKVNUYCUYOUYEUYGXHUYQUYRXIVUAUYGVULTUXMUWEUWFUJZUVQ
      SZUYRUXMUWEUWJUJZUKZTLMUXMUYDFULUWDUXMUKZUWHVUNUYTVUPVUQUWGVUMUVQUWDUXMUW
      EUWFXJXKVUQUWKVUOUYRUWDUXMUWEUWJXJXLXMUWEUYDUKZVUNUYGVUPVULVURVUMUYFUVQUW
      EUYDUXMUWFXNXKVURVUOUYRUYRUWEUYDUXMUWJXNXLXMXOXPUXTUYSVUBTNUYRUXMUYDUWJXQ
      UWIUYRUKZUXSUYSUWNVUBUWIUYRUXMXRVUSUWMVUALMFULVUSUWLUYTUWHUWIUYRUWKXSXTYA
      XMYBWGYCYDYEYFUWNNUXMYGZYHUVTUVQFUXMUVTUVQVUDFUYJUVSVUEUVOVUGUVMUYJUVNUYM
      UTZVJUVOVUHUVSVUIUTVAYIYJUXMUWPFYKZYHUVTUXRUXNUXOUVTUYAUXQUXNUXPUVTUYAUXQ
      UXNUVTUXTUXQUXNYLZNUXTUVTVVCUXSUWNUVTVVCYLZUXSUWMVVDLMFULUVTUXCUWMUXSVVCU
      VTUXCUWMUXSUXQUXNUVTUXCTZUWMUXSTZTZUXQTZUWGUVQUXMVVFUWHVVEUXQUWHUWLUXSYMY
      NVVHUXMUWGRZUXMVUDRZUWDUXMBUJZUWEVEVFZVVHUYJUWDVUDRZUXGUXHVVIVVJVVLTVSVVE
      UYJVVFUXQUVOUYJUVSUXCVVAUQUQVVEVVMVVFUXQUVOUXAVVMUVSUXBUVOUXATUWDFVUDUVOU
      XAURUVOVUHUXAVUIUTYOXAUQVVEUXGVVFUXQUXBUXGUVTUXAUXJYPUQVVEUXHVVFUXQUXBUXH
      UVTUXAUXKYPUQUXMBUWDUWEVUDVUFYQVRVVEUXQVVJVVFUVTUXQVVJUXCUVOUXQVVJUVSUVOU
      XQTUXMFVUDUVOUXQURUVOVUHUXQVUIUTYOYRYRYRVVHUWDUXMAUJZVVKUWEVEVVHAYSZBVUDV
      UDWIZYTZBASZVVMVVJVVNVVKUKVVEVVOVVFUXQUVOVVOUVSUXCUVMVVOUVNUVMEEWIZVCAUUA
      VVOAEHUUBVVSVCAUUCUUDUTUQUQVVEVVQVVFUXQUVOVVQUVSUXCUVMVVQUVNUVMUYJVVPVCBU
      UAVVQUYMBVUDVUFUUBVVPVCBUUEUUFUTUQUQVVRVVHBUYLAGAUYKUUGUUHUUIVVHUWDFVUDVV
      EUXAVVFUXQUVTUXAUXBWMUQVVEVUHVVFUXQUVOVUHUVSUXCVUIUQUQZYOVVHUXMFVUDVVGUXQ
      URVVTYOUWDUXMVUDVUDABUUJUUKVVHUXMUWKRZVVNUWEVEVFZVVFVWAVVEUXQUWLUXSVWAUWH
      UWLUXSTUXMUWIUWKUWLUXSURUWLUXSUULYOUUMYNVVHUVMUXFVUCUXGUXHTZVWAVWBVSVVEUV
      MVVFUXQUVMUVNUVSUXCWQUQVVEUXFVVFUXQUVOUXAUXFUVSUXBUVNUXAUXFUVMUXIUUMXAUQV
      VEUXQVUCVVFUVTUXQVUCUXCUVOUXQVUCUVSUVNUXQVUCUVMFEUXMVHUUMYRYRYRVVEVWCVVFU
      XQUXBVWCUVTUXAUWEUUNYPUQUXMAUWDUWEEHUUOUUPUUQUURUUSXCUUTUVAWDUVBUVCUVDUVE
      UXPUYAVUTUVFUVGVVBUVHUVIUVJUWPUVQFCUWBUWBWSUVKVRUVLUVIUVJ $.
      $( [1-Dec-2010] $) $( [19-Aug-2009] $)
  $}


