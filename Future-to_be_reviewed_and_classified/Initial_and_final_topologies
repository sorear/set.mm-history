$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Initial and final topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c subSp $. $( Function returning a subspace topology. $)

  $( Extend class notation with the function returning a subspace topology. $)
  csubsp $a class subSp $.


  ${
    $d x y z w $.
    $( Function returning the subspace topology induced by the topology ` y `
       and the set ` x ` . $)
    df-subsp $a |- subSp = ( x e. _V , y e. Top |->
                             { z | E. w e. y z = ( w i^i x ) } ) $.
  $}

  ${
    $d A w x y z $.  $d J w x y $.
    $( The subspace topology induced by the topology ` J ` on the set ` A ` .
       (Contributed by FL, 20-Sep-2010.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    subspval $p |- ( ( J e. Top /\ A e. V )
       -> ( subSp ` <. A , J >. ) = { x | E. y e. J x = ( y i^i A ) } ) $=
      ( vz vw ctop wcel wa cop csubsp cv cin wceq wrex cab cvv wss abbidv simpl
      cfv co df-opr elisset adantl cpw inss2 visset inex1 elpw mpbir r19.23aivw
      eleq1 mpbiri abssi pwexg ssexg sylancr ineq2 eqeq2d rexbidv rexeq ovmpt2g
      syl df-subsp syl111anc syl5eqr ) DHIZCEIZJZCDKLUBCDLUCZAMZBMZCNZOZBDPZAQZ
      CDLUDVKCRIZVIVRRIZVLVROVJVSVICEUEUFZVIVJUAVKVSVTWAVSVRCUGZSWBRIVTVQAWBVPV
      MWBIZBDVPWCVOWBIZWDVOCSVNCUHVOCVNCBUIUJUKULVMVOWBUNUOUMUPCRUQVRWBRURUSVEF
      GCDRHVMVNFMZNZOZBGMZPZAQVRLVPBWHPZAQRWECOZWIWJAWKWGVPBWHWKWFVOVMWECVNUTVA
      VBTWHDOWJVQAVPBWHDVCTFGABVFVDVGVH $.
      $( [15-Dec-2013] $) $( [20-Sep-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d J x y $.
    $( The predicate "is an open set of a subspace topology".  (Contributed by
       FL, 28-Jan-2009.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    elsubsp $p |- ( ( J e. Top /\ B e. V ) ->
      ( A e. ( subSp ` <. B , J >. ) <-> E. x e. J A = ( x i^i B ) ) ) $=
      ( vy ctop wcel wa cop csubsp cfv cv cin wceq wrex cab subspval eleq2d cvv
      elisset visset inex1 eleq1 mpbiri r19.23aivw eqeq1 elabg pm5.21nii syl6bb
      rexbidv ) DGHCEHIZBCDJKLZHBFMZAMZCNZOZADPZFQZHZBUPOZADPZULUMUSBFACDERSUTB
      THZVBBUSUAVAVCADVAVCUPTHUOCAUBUCBUPTUDUEUFURVBFBTUNBOUQVAADUNBUPUGUKUHUIU
      J $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d x A $.  $d x J $.  $d x S $.
    $( Sufficient condition for being an open set in a subspace.  (Contributed
       by Jeff Hankins, 11-Jul-2009.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    elsubspr $p |- ( ( J e. Top /\ S e. V /\ A e. J ) ->
                  ( A i^i S ) e. ( subSp ` <. S , J >. ) ) $=
      ( vx ctop wcel cin cop csubsp cfv wa cv wceq wrex eqid ineq1 eqeq2d mpan2
      rcla4ev elsubsp syl5ibr 3impia ) CFGZBDGZACGZABHZBCIJKGZUFUHUDUELUGEMZBHZ
      NZECOZUFUGUGNZULUGPUKUMEACUIANUJUGUGUIABQRTSEUGBCDUAUBUC $.
      $( [15-Dec-2013] $) $( [11-Jul-2009] $)
  $}

  ${
    $d o t J $.  $d o t X $.
    subspid.1 $e |- X = U. J $.
    $( The subspace topology of the base set is the original topology.
       (Contributed by Jeff Hankins, 9-Jul-2009.) $)
    subspid $p |- ( J e. Top -> ( subSp ` <. X , J >. ) = J ) $=
      ( vo vt ctop wcel cop csubsp cfv cv cin wceq wrex wb cuni uniexg syl5eqel
      cvv wa elsubsp mpdan topopn inopn mpd3an3 eleq1 syl5ibrcom r19.23adva wss
      adantr simpr eltopss df-ss sylib eqcomd eqeq2d rcla4ev syl11anc ex impbid
      ineq1 bitrd eqrdv ) AFGZDBAHIJZAVDDKZVEGZVFEKZBLZMZEANZVFAGZVDBSGVGVKOVDB
      APSCAFQREVFBASUAUBVDVKVLVDVJVLEAVDVHAGZTVLVJVIAGZVDVMBAGZVNVDVOVMABCUCUJV
      HBAUDUEVFVIAUFUGUHVDVLVKVDVLTZVLVFVFBLZMZVKVDVLUKVPVQVFVPVFBUIVQVFMVFABCU
      LVFBUMUNUOVJVREVFAVHVFMVIVQVFVHVFBVAUPUQURUSUTVBVC $.
      $( [15-Dec-2013] $) $( [9-Jul-2009] $)
  $}

  ${
    $d x y A $.  $d x y J $.
    $( A subspace topology is a topology.  (Contributed by FL, 5-Jan-2009.)
       (Revised by Mario Carneiro, 15-Dec-2013.) $)
    stoig3 $p |- ( ( J e. Top /\ A e. V ) -> ( subSp ` <. A , J >. ) e. Top )
        $=
      ( vx vy ctop wcel cop csubsp cfv cin wceq wrex cab subspval subtop adantr
      wa cv eqeltrd ) BFGZACGZRABHIJDSESAKLEBMDNZFDEABCOUAUCFGUBDEBAPQT $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)

    $( The underlying set of a subspace topology.  (Contributed by FL,
       5-Jan-2009.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    stoig2 $p |- ( ( J e. Top /\ A C_ U. J ) ->
                   U. ( subSp ` <. A , J >. ) = A ) $=
      ( vx vy ctop wcel cuni wss wa cop csubsp cfv cv cin wceq wrex cab cvv syl
      id uniexg ssexg syl2anc subspval syldan unieqd cpw inss2 inex1 elpw mpbir
      visset eleq1 mpbiri r19.23aivw abssi sspwuni mpbi a1i eqsstrd simpr sylib
      sseqin2 eqcomd simpl eqid topopn elsubspr syl111anc eqeltrd elssuni eqssd
      ) BEFZABGZHZIZABJKLZGZAVPVRCMZDMZANZOZDBPZCQZGZAVPVQWDVMVOARFZVQWDOVOVOVN
      RFWFVMVOTBEUAAVNRUBUCZCDABRUDUEUFWEAHZVPWDAUGZHWHWCCWIWBVSWIFZDBWBWJWAWIF
      ZWKWAAHVTAUHWAAVTADULUIUJUKVSWAWIUMUNUOUPWDAUQURUSUTVPAVQFAVRHVPAVNANZVQV
      PWLAVPVOWLAOVMVOVAAVNVCVBVDVPVMWFVNBFZWLVQFVMVOVEZWGVPVMWMWNBVNVNVFVGSVNA
      BRVHVIVJAVQVKSVL $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  $( The topological space built with a subspace topology.  (Contributed by FL,
     5-Jan-2009.)  (The proof was shortened by Mario Carneiro, 15-Dec-2013.) $)
  stoig $p |- ( ( J e. Top /\ A C_ U. J ) ->
   { <. ( Base ` ndx ) , A >. ,
       <. ( TopOpen ` ndx ) , ( subSp ` <. A , J >. ) >. } e. TopSp ) $=
    ( ctop wcel cuni wss wa cnx cbs cfv cop cts csubsp cpr ctps wceq cvv uniexg
    wb id ssexg syl2anc fvex eqid eltpsg sylancl stoig3 syldan stoig2 mpbir2and
    eqcomd ) BCDZABEZFZGZHIJAKHLJABKZMJZKNZODZUQCDZAUQEZPZUOAQDZUQQDUSUTVBGSUNU
    NUMQDVCULUNTBCRAUMQUAUBZUPMUCAQQUQURURUDUEUFULUNVCUTVDABQUGUHUOVAAABUIUKUJ
    $.
    $( [15-Dec-2013] $) $( [21-Oct-2012] $)

  ${
    $d w x y z B $.  $d w x y A $.  $d w x y z J $.
    $( Composition of subspaces.  (Contributed by Mario Carneiro,
       15-Dec-2013.) $)
    subspco $p |- ( ( J e. Top /\ A e. V /\ B e. W ) ->
                     ( subSp ` <. A , ( subSp ` <. B , J >. ) >. ) =
                       ( subSp ` <. ( A i^i B ) , J >. ) ) $=
      ( vx vy vz vw ctop wcel cv cin wceq wrex cab cop csubsp cfv subspval in32
      visset inex1 ineq1 inass eqtri syl6eq abrexco simp1 simp3 syl11anc opeq2d
      w3a fveq2d stoig3 3adant2 eqeltrrd simp2 eqtrd cvv inex1g syl 3eqtr4a ) C
      JKZADKZBEKZUMZFLZGLZAMZNGHLILZBMZNICOHPZOFPZVHVKABMZMZNICOFPZABCQRSZQZRSZ
      VOCQRSZFGHICVLVJVPVKBIUBUCVIVLNVJVLAMZVPVIVLAUDWBVKAMBMVPVKBAUAVKABUEUFUG
      UHVGVTAVMQZRSZVNVGVSWCRVGVRVMAVGVDVFVRVMNVDVEVFUIZVDVEVFUJHIBCETUKZULUNVG
      VMJKVEWDVNNVGVRVMJWFVDVFVRJKVEBCEUOUPUQVDVEVFURZFGAVMDTUKUSVGVDVOUTKZWAVQ
      NWEVGVEWHWGABDVAVBFIVOCUTTUKVC $.
      $( [15-Dec-2013] $)
  $}

  ${
    $d x y J $.  $d x y X $.  $d x y A $.  $d y V $.
    subspint.1 $e |- X = U. J $.
    $( When the subspace region is not a subset of the base of the topology,
       the resulting set is the same as the subspace restricted to the base.
       (Contributed by Mario Carneiro, 15-Dec-2013.) $)
    subspint $p |- ( ( J e. Top /\ A e. V ) ->
      ( subSp ` <. A , J >. ) = ( subSp ` <. ( A i^i X ) , J >. ) ) $=
      ( ctop wcel wa cop csubsp cfv subspid adantr opeq2d fveq2d topopn subspco
      cin wceq mpd3an3 eqtr3d ) BFGZACGZHZADBIJKZIZJKZABIZJKADRBIJKZUDUFUHJUDUE
      BAUBUEBSUCBDELMNOUBUCDBGZUGUISUBUJUCBDEPMADBCBQTUA $.
      $( [15-Dec-2013] $)
  $}

  ${
    $d x y J $.
    $( The subspace topology induced by the topology ` J ` on the empty set.
       (Contributed by FL, 22-Dec-2008.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    subsp0 $p |- ( J e. Top -> ( subSp ` <. (/) , J >. ) = { (/) } ) $=
      ( vx vy ctop wcel c0 cop csubsp cfv csn cv wceq cin wrex cvv wb 0ex mpan2
      elsubsp in0 eqeq2i biimpi r19.23aivw cuni eqid topopn eqcomi ineq1 eqeq2d
      rcla4ev sylancl eqeq1 rexbidv syl5ibrcom impbid2 bitrd elsn syl6bbr eqrdv
      ) ADEZBFAGHIZFJZUTBKZVAEZVCFLZVCVBEUTVDVCCKZFMZLZCANZVEUTFOEVDVIPQCVCFAOS
      RUTVIVEVHVECAVHVEVGFVCVFTUAUBUCUTVIVEFVGLZCANZUTAUDZAEFVLFMZLZVKAVLVLUEUF
      VMFVLTUGVJVNCVLAVFVLLVGVMFVFVLFUHUIUJUKVEVHVJCAVCFVGULUMUNUOUPBFUQURUS $.
      $( [15-Dec-2013] $) $( [22-Dec-2008] $)
  $}

  ${
    $d A x y $.  $d V x $.
    $( The only subspace topology induced by the topology ` { (/) } ` .
       (Contributed by FL, 5-Jan-2009.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    subspsn $p |- ( A e. V -> ( subSp ` <. A , { (/) } >. ) = { (/) } ) $=
      ( vx vy wcel c0 csn cop csubsp cfv wceq cin wrex ctop sn0top elsubsp mpan
      cv wb 0ex ineq1 eqeq2d rexsn incom eqtri eqeq2i bitri syl6bb elsn syl6bbr
      in0 eqrdv ) ABEZCAFGZHIJZUNUMCRZUOEZUPFKZUPUNEUMUQUPDRZALZKZDUNMZURUNNEUM
      UQVBSODUPAUNBPQVBUPFALZKZURVAVDDFTUSFKUTVCUPUSFAUAUBUCVCFUPVCAFLFFAUDAUKU
      EUFUGUHCFUIUJUL $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d A x y $.  $d J x y $.  $d X x $.
    subspsn2.1 $e |- X = U. J $.
    $( The subspace topology induced by a singleton.  (Contributed by FL,
       5-Jan-2009.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    subspsn2 $p |- ( ( J e. Top /\ A e. X ) ->
      ( subSp ` <. { A } , J >. ) = { (/) , { A } } ) $=
      ( ctop wcel wa csn cop csubsp cfv cuni wceq c0 cpr cvv snex stoig3 adantr
      mpan2 wss snssi eleq2s stoig2 sylan2 eqid topsn syl11anc ) BEFZACFZGAHZBI
      JKZEFZULLZUKMZULNUKOMUIUMUJUIUKPFUMAQUKBPRTSUJUIUKBLZUAZUOUQAUPCAUPUBDUCU
      KBUDUEAULUNUNUFUGUH $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d o x A $.  $d o x J $.  $d o x S $.  $d o x X $.
    subcld.1 $e |- X = U. J $.
    $( A closed set of a subspace topology is a closed set of the original
       topology intersected with the subset.  (Contributed by FL,
       11-Jul-2009.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    subcld $p |- ( ( J e. Top /\ S C_ X ) -> ( A e. ( Clsd ` ( subSp ` <. S , J
    >. ) ) <-> E. x e. ( Clsd ` J ) A = ( x i^i S ) ) ) $=
      ( vo ctop wcel wss wa cfv cdif cin wceq wb cvv biimpi cun c0 ccld cuni cv
      cop csubsp wrex uniexg sseq2i ssexg ancoms syl2an stoig3 syldan iscld syl
      eqid stoig2 sseq2d difeq1d eleq1d anbi12d sylan2b elsubsp syl11anc anbi2d
      simpl opncld adantlr adantr incom syl5eq ad2antlr ad2antrr difeq2 difindi
      df-ss difid uneq2i un0 3eqtri syl6eq adantl dfss4 3eqtr2rd difeq1i indif2
      3eqtr2i ineq1 eqeq2d rcla4ev ex r19.23adva expimpd sylbid difin2 elsubspr
      simpll cldopn syl111anc eqeltrd inss2 jctil syl5ibrcom impbid 3bitrd
      sseq1 ) DHIZCEJZKZBCDUDUELZUALIZBXJUBZJZXLBMZXJIZKZBCJZCBMZXJIZKZBAUCZCNZ
      OZADUALZUFZXIXJHIZXKXPPXGXHCQIZYFXGDUBZQIZCYHJZYGXHDHUGXHYJEYHCFUHZRYJYIY
      GCYHQUIUJUKZCDQULUMBXJXLXLUPUNUOXHXGYJXPXTPYKXGYJKZXMXQXOXSYMXLCBCDUQZURY
      MXNXRXJYMXLCBYNUSUTVAVBXIXTYEXIXTXQXRGUCZCNZOZGDUFZKYEXIXSYRXQXIXGYGXSYRP
      XGXHVFYLGXRCDQVCVDVEXIXQYRYEXIXQKZYQYEGDYSYODIZKZYQYEUUAYQKZEYOMZYDIZBUUC
      CNZOZYEUUAUUDYQXIYTUUDXQXGYTUUDXHYODEFVGVHVHVIUUBBECNZYOMZUUEUUBUUHCYOMZC
      XRMZBUUBUUGCYOYSUUGCOZYTYQXHUUKXGXQXHUUGCENZCECVJZXHUULCOCEVPRVKVLVMUSYQU
      UJUUIOUUAYQUUJCYPMZUUIXRYPCVNUUNUUICCMZSUUITSUUICYOCVOUUOTUUICVQZVRUUIVSV
      TWAWBYSUUJBOZYTYQXQUUQXIXQUUQBCWCRWBVMWDUUHUULYOMCUUCNUUEUUGUULYOUUMWECEY
      OWFCUUCVJWGWAYCUUFAUUCYDYAUUCOYBUUEBYAUUCCWHWIWJVDWKWLWMWNXIYCXTAYDXIYAYD
      IZKZXTYCYBCJZCYBMZXJIZKUUSUVBUUTUUSUVAEYAMZCNZXJXIUVAUVDOUURXIUVACYAMZUVD
      UVAUVEUUOSUVETSUVECYACVOUUOTUVEUUPVRUVEVSVTXHUVEUVDOXGCYAEWOWBVKVIUUSXGYG
      UVCDIZUVDXJIXGXHUURWQXIYGUURYLVIXGUURUVFXHYADEFWRVHUVCCDQWPWSWTYACXAXBYCX
      QUUTXSUVBBYBCXFYCXRUVAXJBYBCVNUTVAXCWLXDXE $.
      $( [15-Dec-2013] $) $( [11-Jul-2009] $)
  $}

  ${
    $d o r s t x y z A $.  $d o r s t x y z C $.  $d o r s t x y z D $.
    $d o r s t x y z J $.  $d o r s t x y z K $.  $d o r s t x y z X $.
    $d o r s t x y z Y $.
    subtopmet.1 $e |- D = ( C |` ( Y X. Y ) ) $.
    subtopmet.2 $e |- X = dom dom C $.
    subtopmet.j $e |- J = ( MetOpen ` C ) $.
    subtopmet.k $e |- K = ( MetOpen ` D ) $.
    ${
      subtopmet.a $e |- A e. _V $.
      $( Lemma for ~ subtopmet . $)
      subtopmetlem $p |- ( ( C e. Met /\ Y C_ X ) -> ( A e. ( subSp ` <. Y , J
      >. ) -> A e. K ) ) $=
        ( cme wcel wss wa wceq cdm 3ad2ant1 adantr vy vx vo vr cin wrex cbl cfv
        vz cv crn wral cop csubsp inss2 sseq1 mpbiri 3ad2ant3 basmetres sseqtrd
        w3a cc0 clt wbr co simpl1l simp2 inss1 ssel2 sylan opni2 syl111anc cres
        cr cxp metres syl5eqel 3adant3 syl3an1 eleqtrd simp3 simprl eqid blelrn
        wi syl22anc blcntr simplrr wfun wfn wf metf ffun ad2antrr resss eqsstri
        syl funss mpsyl dmeqi dmres eqtri simp11r syl11anc sseqtr4d df-ss sylib
        xpss12 fdm syl5eq df-fn sylanbrc a1i eqimss2 sseld imp adantrr syl32anc
        oprssoprv simprr eqbrtrd wb simp11l dmss ax-mp sseli syl6eleqr ad2antrl
        sseldd simpll3 simplrl elbl2 mpbird ex syl6sseq 3imtr4d r19.23adv dmexg
        dmxpid cvv syl5eqss adantrd jcad elin syl6ibr elbl simpl13 eleq2d ssrdv
        eleq2 anbi12d rcla4ev syl12anc 3expia mpd r19.21aiva 3exp opntop sseq2i
        jca ctop ssexg sylan2 sylanb ancoms elsubsp isopn ) BMNZGFOZPZAUAUJZGUE
        ZQZUADUFZACRZRZOZUBUJZUCUJZNZUVSAOZPZUCCUGUHZUKZUFZUBAULZPZAGDUMUNUHNZA
        ENZUVJUVMUWGUADUVJUVKDNZUVMUWGUVJUWJUVMVAZUVQUWFUWKAGUVPUVMUVJAGOZUWJUV
        MUWLUVLGOUVKGUOAUVLGUPUQURZUVJUWJGUVPQZUVMBCFGHIUSSZUTUWKUWEUBAUWKUVRAN
        ZPZVBUDUJZVCVDZUVRUWRBUGUHVEZUVKOZPZUDVNUFZUWEUWQUVHUWJUVRUVKNZUXCUVHUV
        IUWJUVMUWPVFUWKUWJUWPUVJUWJUVMVGTUWKAUVKOZUWPUXDUVMUVJUXEUWJUVMUXEUVLUV
        KOUVKGVHAUVLUVKUPUQURAUVKUVRVIVJUDUVKBUVRDJVKVLUWQUXBUWEUDVNUWKUWPUWRVN
        NZUXBUWEWEUWKUWPUXFVAZUXBUWEUXGUXBPZUVRUWRUWCVEZUWDNZUVRUXINZUXIAOZUWEU
        XHCMNZUVRUVPNZUXFUWSUXJUXGUXMUXBUWKUWPUXMUXFUVJUWJUXMUVMUVHUXMUVIUVHCBG
        GVOZVMZMHBGVPZVQTSSTZUXHUVRGUVPUXGUVRGNZUXBUWKUWLUWPUXFUXSUWMUWLUWPUXSU
        XFAGUVRVIVRVSZTUXGUWNUXBUWKUWPUWNUXFUWOSTZVTZUXGUXFUXBUWKUWPUXFWATZUXGU
        WSUXAWBZCUVRUWRUVPUVPWCZWDWFUXHUXMUXNUXFUWSUXKUXRUYBUYCUYDCUVRUWRUVPUYE
        WGWFUXHUIUXIAUXHUIUJZUVPNZUVRUYFCVEZUWRVCVDZPZUYFUVLNZUYFUXINZUYFANUXHU
        YJUYFUVKNZUYFGNZPUYKUXHUYJUYMUYNUXHUYJUYMUXHUYJPZUWTUVKUYFUXGUWSUXAUYJW
        HUYOUYFUWTNZUVRUYFBVEZUWRVCVDZUYOUYQUYHUWRVCUYOBWIZCUXOWJZCBOZUXSUYNUYQ
        UYHQUXGUYSUXBUYJUWKUWPUYSUXFUVJUWJUYSUVMUVHUYSUVIUVHFFVOZVNBWKZUYSBFIWL
        ZVUBVNBWMWQTSSWNZUYOCWIZUVOUXOQUYTVUAUYOUYSVUFCUXPBHBUXOWOWPZVUECBWRWSU
        YOUVOUXOBRZUEZUXOUVOUXPRVUICUXPHWTBUXOXAXBZUYOUXOVUHOVUIUXOQUYOUXOVUBVU
        HUYOUVIUVIUXOVUBOUXGUVIUXBUYJUVHUVIUWJUVMUWPUXFXCWNZVUKGFGFXHXDUXGVUHVU
        BQZUXBUYJUWKUWPVULUXFUVJUWJVULUVMUVHVULUVIUVHVUCVULVUDVUBVNBXIWQTSSWNXE
        UXOVUHXFXGXJCUXOXKXLVUAUYOVUGXMUXGUXSUXBUYJUXTWNZUXHUYGUYNUYIUXHUYGUYNU
        XHUVPGUYFUXHUWNUVPGOUYAUVPGXNWQXOXPXQUVRUYFGGBCXSXRUXHUYGUYIXTYAUYOUVHU
        VRFNUYFFNZUXFUWSUYPUYRYBUXGUVHUXBUYJUVHUVIUWJUVMUWPUXFYCWNUYOGFUVRVUKVU
        MYIUYGVUNUXHUYIUYGUYFVUHRZFUVPVUOUYFUVOVUHOZUVPVUOOVUAVUPVUGCBYDYEUVOVU
        HYDYEYFIYGYHUWKUWPUXFUXBUYJYJUXGUWSUXAUYJYKUYFBUVRUWRFIYLXRYMYIYNUXHUYG
        UYNUYIUXHUVPGUYFUXHUVPVUIRZGUVOVUIVUJWTUXHVUQUXORZGVUQVUROZUXHVUIUXOOVU
        SUXOVUHVHVUIUXOYDYEXMGYSYOUUAXOUUBUUCUYFUVKGUUDUUEUXHUXMUXNUXFUWSUYLUYJ
        YBUXRUYBUYCUYDUYFCUVRUWRUVPUYEUUFWFUXHAUVLUYFUVJUWJUVMUWPUXFUXBUUGUUHYP
        UUIUWBUXKUXLPUCUXIUWDUVSUXIQUVTUXKUWAUXLUVSUXIUVRUUJUVSUXIAUPUUKUULUUMY
        NUUNYQUUOUUPUUTUUQYQUVJDUVANZGYTNZUWHUVNYBUVHVUTUVIBDJUURTUVIUVHVVAUVIG
        VUOOZUVHVVAFVUOGIUUSUVHVVBVUOYTNZVVAUVHVUHYTNVVCBMYRVUHYTYRWQGVUOYTUVBU
        VCUVDUVEUAAGDYTUVFXDUVJUXMUWIUWGYBUVJCUXPMHUVHUXPMNUVIUXQTVQUBUCACEUVPU
        YEKUVGWQYP $.
        $( [15-Dec-2013] $) $( [21-Aug-2009] $)
    $}

    $( Two alternate formulations of a subspace topology of a metric space
       topology.  (Contributed by Jeff Hankins, 21-Aug-2009.) $)
    subtopmet $p |- ( ( C e. Met /\ Y C_ X ) -> ( subSp ` <. Y , J >. ) = K )
        $=
      ( vy vr wcel wa co wceq crp cr ad2antrr adantr vt vx vz vs cme wss csubsp
      cop cfv cv visset subtopmetlem cbl wrex cab cuni cin wex cc0 clt wbr cres
      cxp metres syl5eqel simprl simprr opni2 syl111anc simplll simplr cdm eqid
      opnss sylan ad2ant2r sseqtr4d sseldd simprrl blcntr syl22anc elrp biimpri
      adantrr adantl simprrr eqidd opreq1 sseq1d eqeq2d anbi12d opreq2 rcla42ev
      basmetres syl112anc oprex eleq2 anbi2d 2rexbidv cla4ev syl11anc r19.23adv
      eqeq1 exp32 mpd expr eluniab syl6ibr sseld jcad biimpi wi simpll ad2antlr
      elin wb ad3antrrr simpr eleqtrd rpre ad2antll rpgt0 elbl adantlr wfun wfn
      wf metf ffun syl 3syl adantll ssel2 r19.23advv impbid eqrdv ctop 3ad2ant2
      ffn cvv resss eqsstri a1i oprssoprv syl32anc simpl rpregt0 elbl2 syl31anc
      mpbid eqbrtrrd mpbir2and exp53 com14 imp com12 imp3a sylani syl5bi opntop
      19.23adv id uniopn2 uniexg eqeltrrd ssexg syl2anc w3a simp1ll blopn eleq1
      3adant3 3ad2ant3 mpbird 3exp abssdv uniopn elsubspr eqeltrd ex ) AUEMZFEU
      FZNZUAFCUHUGUIZDUWCUAUJZUWDMZUWEDMZUWEABCDEFGHIJUAUKULUWCUWGUWFUWCUWGNZUW
      EKUJZLUJZBUMUIZOZUWEUFZUBUJZUWIUWJAUMUIZOZPZNZLQUNKFUNZUBUOZUPZFUQZUWDUWH
      UCUWEUXBUWHUCUJZUWEMZUXCUXBMZUWHUXDUXCUXAMZUXCFMZNZUXEUWHUXDUXFUXGUWHUXDU
      XCUWNMZUWSNZUBURZUXFUWCUWGUXDUXKUWCUWGUXDNZNZUSUDUJZUTVAZUXCUXNUWKOZUWEUF
      ZNZUDRUNZUXKUXMBUEMZUWGUXDUXSUWAUXTUWBUXLUWABAFFVCZVBZUEGAFVDVEZSUWCUWGUX
      DVFUWCUWGUXDVGZUDUWEBUXCDJVHVIUXMUXRUXKUDRUXMUXNRMZUXRUXKUXMUYEUXRNZNZUXC
      UXCUXNUWOOZMZUWMUYHUWPPZNZLQUNKFUNZUXKUYGUWAUXCEMZUYEUXOUYIUWAUWBUXLUYFVJ
      UXMUYMUYFUXMFEUXCUWAUWBUXLVKUXMUWEFUXCUXMUWEBVLVLZFUWAUWGUWEUYNUFZUWBUXDU
      WAUXTUWGUYOUYCUWEBDUYNUYNVMZJVNZVOVPUWCFUYNPZUXLABEFGHWNZTVQUYDVRZVRTUXMU
      YEUXRVFUXMUYEUXOUXQVSAUXCUXNEHVTWAUYGUXGUXNQMZUXQUYHUYHPZUYLUXMUXGUYFUYTT
      UYFVUAUXMUYEUXOVUAUXQVUAUYEUXONUXNWBWCWDWEUXMUYEUXOUXQWFUYGUYHWGUYKUXQVUB
      NUXCUWJUWKOZUWEUFZUYHUXCUWJUWOOZPZNKLUXCUXNFQUWIUXCPZUWMVUDUYJVUFVUGUWLVU
      CUWEUWIUXCUWJUWKWHWIVUGUWPVUEUYHUWIUXCUWJUWOWHWJWKUWJUXNPZVUDUXQVUFVUBVUH
      VUCUXPUWEUWJUXNUXCUWKWLWIVUHVUEUYHUYHUWJUXNUXCUWOWLWJWKWMWOUXJUYIUYLNUBUY
      HUXCUXNUWOWPUWNUYHPZUXIUYIUWSUYLUWNUYHUXCWQVUIUWRUYKKLFQVUIUWQUYJUWMUWNUY
      HUWPXCWRWSWKWTXAXDXBXEXFUWSUBUXCXGZXHUWHUWEFUXCUWHUWEUYNFUWCUXTUWGUYOUWAU
      XTUWBUYCTUYQVOUWCUYRUWGUYSTVQXIXJUXCUXAFXOZXHUXEUXHUWHUXDVUKUXFUWHUXKUXGU
      XDUXFUXKVUJXKUWHUXKUXGUXDUWHUXJUXGUXDXLZUBUXJUWHVULUXIUWSUWHVULXLZUXIUWRV
      UMKLFQUWHUWIFMZUWJQMZNZUWRUXIVULUWHVUPUWRUXIUXGUXDUWHVUPNZUWRUXINZNZUXGNZ
      UWLUWEUXCVURUWMVUQUXGUWMUWQUXIXMXNVUTUXCUWLMZUXCUYNMZUWIUXCBOZUWJUTVAZVUT
      UXTUWIUYNMZUWJRMZUSUWJUTVAZVVAVVBVVDNXPVUQUXTVURUXGUWAUXTUWBUWGVUPUYCXQSV
      UQVVEVURUXGUWCVUNVVEUWGVUOUWCVUNNUWIFUYNUWCVUNXRUWCUYRVUNUYSTXSVPSVUQVVFV
      URUXGVUOVVFUWHVUNUWJXTZYASVUQVVGVURUXGVUOVVGUWHVUNUWJYBZYASUXCBUWIUWJUYNU
      YPYCWAVUQUXGVVBVURUWHUXGVVBVUPUWCUXGVVBUWGUWCUXGNUXCFUYNUWCUXGXRUWCUYRUXG
      UYSTXSYDYDYDVUTUWIUXCAOZVVCUWJUTVUTAYEZBUYNUYNVCZYFZBAUFZVVEVVBVVJVVCPVUQ
      VVKVURUXGUWAVVKUWBUWGVUPUWAEEVCZRAYGVVKAEHYHVVORAYIYJXQSVUQVVMVURUXGUWAVV
      MUWBUWGVUPUWAUXTVVLRBYGVVMUYCBUYNUYPYHVVLRBYSYKXQSVVNVUTBUYBAGAUYAUUAUUBU
      UCVUTUWIFUYNVUQVUNVURUXGUWHVUNVUOVFSVUQUYRVURUXGUWCUYRUWGVUPUYSSSZXSVUTUX
      CFUYNVUSUXGXRVVPXSUWIUXCUYNUYNABUUDUUEVUTUXCUWPMZVVJUWJUTVAZVURVVQVUQUXGU
      WQUXIVVQUWMUWQUXINUXCUWNUWPUWQUXIXRUWQUXIUUFXSYLXNVUTUWAUWIEMZUYMVVFVVGNZ
      VVQVVRXPVUQUWAVURUXGUWAUWBUWGVUPVJSVUQVVSVURUXGUWCVUNVVSUWGVUOUWBVUNVVSUW
      AFEUWIYMZYLVPSVUQUXGUYMVURUWHUXGUYMVUPUWCUXGUYMUWGUWBUXGUYMUWAFEUXCYMYLYD
      YDYDVUQVVTVURUXGVUOVVTUWHVUNUWJUUGYASUXCAUWIUWJEHUUHUUIUUJUUKUULVRUUMUUNY
      NUUOUUPUVAUUQUURUUSYOYPUWHCYQMZFYTMZUXACMZUXBUWDMUWAVWBUWBUWGACIUUTZSZUWC
      VWCUWGUWBUWBEYTMVWCUWAUWBUVBUWACUPZEYTACEHIUVCUWAVWBVWGYTMVWECYQUVDYJUVEF
      EYTUVFUVGTUWHVWBUWTCUFVWDVWFUWHUWSUBCUWHUWRUWNCMZKLFQUWHVUPUWRVWHUWHVUPUW
      RUVHZVWHUWPCMZVWIUWAVVSVVFVVGVWJUWAUWBUWGVUPUWRUVIUWHVUPVVSUWRUWHVUNVVSVU
      OUWHUWBVUNVVSUWAUWBUWGVKVWAVOWDUVLVUPUWHVVFUWRVUOVVFVUNVVHWEYRVUPUWHVVGUW
      RVUOVVGVUNVVIWEYRAUWIUWJCEHIUVJWAUWRUWHVWHVWJXPZVUPUWQVWKUWMUWNUWPCUVKWEU
      VMUVNUVOYNUVPUWTCUVQXAUXAFCYTUVRVIUVSUVTYOYP $.
      $( [15-Dec-2013] $) $( [19-Aug-2009] $)
  $}


