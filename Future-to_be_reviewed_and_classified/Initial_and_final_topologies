$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Initial and final topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c subSp $. $( Function returning a subspace topology. $)

  $( Extend class notation with the function returning a subspace topology. $)
  csubsp $a class subSp $.


  ${
    $d x y z w $.
    $( Function returning the subspace topology induced by the topology ` y `
       and the set ` x ` . $)
    df-subsp $a |- subSp = ( x e. _V , y e. Top |->
                             { z | E. w e. y z = ( w i^i x ) } ) $.
  $}

  ${
    $d A w x y z $.  $d J w x y $.
    $( The subspace topology induced by the topology ` J ` on the set ` A ` .
       (Contributed by FL, 20-Sep-2010.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    subspval $p |- ( ( J e. Top /\ A e. V )
       -> ( subSp ` <. A , J >. ) = { x | E. y e. J x = ( y i^i A ) } ) $=
      ( vz vw ctop wcel wa cop csubsp cfv co cv cin wceq wrex cab df-opr cvv
      elisset adantl simpl cpw wss inss2 visset inex1 elpw mpbir eleq1 mpbiri
      r19.23aivw abssi pwexg ssexg sylancr syl ineq2 eqeq2d rexbidv abbidv
      rexeq df-subsp ovmpt2g syl111anc syl5eqr ) DHIZCEIZJZCDKLMCDLNZAOZBOZCPZQ
      ZBDRZASZCDLTVKCUAIZVIVRUAIZVLVRQVJVSVICEUBUCZVIVJUDVKVSVTWAVSVRCUEZUFWBUA
      IVTVQAWBVPVMWBIZBDVPWCVOWBIZWDVOCUFVNCUGVOCVNCBUHUIUJUKVMVOWBULUMUNUOCUAU
      PVRWBUAUQURUSFGCDUAHVMVNFOZPZQZBGOZRZASVRLVPBWHRZASUAWECQZWIWJAWKWGVPBWHW
      KWFVOVMWECVNUTVAVBVCWHDQWJVQAVPBWHDVDVCFGABVEVFVGVH $.
      $( [15-Dec-2013] $) $( [20-Sep-2010] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d J x y $.
    $( The predicate "is an open set of a subspace topology".  (Contributed by
       FL, 28-Jan-2009.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    elsubsp $p |- ( ( J e. Top /\ B e. V ) ->
      ( A e. ( subSp ` <. B , J >. ) <-> E. x e. J A = ( x i^i B ) ) ) $=
      ( vy ctop wcel wa cop csubsp cfv cv cin wceq wrex cab subspval eleq2d
      cvv elisset visset inex1 eleq1 mpbiri r19.23aivw eqeq1 rexbidv elabg
      pm5.21nii syl6bb ) DGHCEHIZBCDJKLZHBFMZAMZCNZOZADPZFQZHZBUPOZADPZULUMUSBF
      ACDERSUTBTHZVBBUSUAVAVCADVAVCUPTHUOCAUBUCBUPTUDUEUFURVBFBTUNBOUQVAADUNBUP
      UGUHUIUJUK $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d x A $.  $d x J $.  $d x S $.
    $( Sufficient condition for being an open set in a subspace.  (Contributed
       by Jeff Hankins, 11-Jul-2009.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    elsubspr $p |- ( ( J e. Top /\ S e. V /\ A e. J ) ->
                  ( A i^i S ) e. ( subSp ` <. S , J >. ) ) $=
      ( vx ctop wcel cin cop csubsp cfv wa cv wceq wrex eqid ineq1 eqeq2d
      rcla4ev mpan2 elsubsp syl5ibr 3impia ) CFGZBDGZACGZABHZBCIJKGZUFUHUDUELUG
      EMZBHZNZECOZUFUGUGNZULUGPUKUMEACUIANUJUGUGUIABQRSTEUGBCDUAUBUC $.
      $( [15-Dec-2013] $) $( [11-Jul-2009] $)
  $}

  ${
    $d o t J $.  $d o t X $.
    subspid.1 $e |- X = U. J $.
    $( The subspace topology of the base set is the original topology.
       (Contributed by Jeff Hankins, 9-Jul-2009.) $)
    subspid $p |- ( J e. Top -> ( subSp ` <. X , J >. ) = J ) $=
      ( vo vt ctop wcel cop csubsp cfv cv cin wceq wrex cvv wb cuni uniexg
      syl5eqel elsubsp mpdan wa topopn adantr inopn mpd3an3 eleq1 syl5ibrcom
      r19.23adva simpr wss eltopss df-ss sylib eqcomd ineq1 eqeq2d rcla4ev
      syl11anc ex impbid bitrd eqrdv ) AFGZDBAHIJZAVDDKZVEGZVFEKZBLZMZEANZVFAGZ
      VDBOGVGVKPVDBAQOCAFRSEVFBAOTUAVDVKVLVDVJVLEAVDVHAGZUBVLVJVIAGZVDVMBAGZVNV
      DVOVMABCUCUDVHBAUEUFVFVIAUGUHUIVDVLVKVDVLUBZVLVFVFBLZMZVKVDVLUJVPVQVFVPVF
      BUKVQVFMVFABCULVFBUMUNUOVJVREVFAVHVFMVIVQVFVHVFBUPUQURUSUTVAVBVC $.
      $( [15-Dec-2013] $) $( [9-Jul-2009] $)
  $}

  ${
    $d x y A $.  $d x y J $.
    $( A subspace topology is a topology.  (Contributed by FL, 5-Jan-2009.)
       (Revised by Mario Carneiro, 15-Dec-2013.) $)
    stoig3 $p |- ( ( J e. Top /\ A e. V ) -> ( subSp ` <. A , J >. ) e. Top )
        $=
      ( vx vy ctop wcel wa cop csubsp cfv cv cin wceq wrex cab subspval subtop
      adantr eqeltrd ) BFGZACGZHABIJKDLELAMNEBODPZFDEABCQUAUCFGUBDEBARST $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)

    $( The underlying set of a subspace topology.  (Contributed by FL,
       5-Jan-2009.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    stoig2 $p |- ( ( J e. Top /\ A C_ U. J ) ->
                   U. ( subSp ` <. A , J >. ) = A ) $=
      ( vx vy ctop wcel cuni wss wa cop csubsp cfv cv cin wceq wrex cab cvv id
      uniexg ssexg syl2anc subspval syldan unieqd cpw inss2 visset inex1 elpw
      mpbir eleq1 mpbiri r19.23aivw abssi sspwuni mpbi a1i eqsstrd simpr
      sseqin2 sylib eqcomd simpl eqid topopn syl elsubspr syl111anc eqeltrd
      elssuni eqssd ) BEFZABGZHZIZABJKLZGZAVPVRCMZDMZANZOZDBPZCQZGZAVPVQWDVMVOA
      RFZVQWDOVOVOVNRFWFVMVOSBETAVNRUAUBZCDABRUCUDUEWEAHZVPWDAUFZHWHWCCWIWBVSWI
      FZDBWBWJWAWIFZWKWAAHVTAUGWAAVTADUHUIUJUKVSWAWIULUMUNUOWDAUPUQURUSVPAVQFAV
      RHVPAVNANZVQVPWLAVPVOWLAOVMVOUTAVNVAVBVCVPVMWFVNBFZWLVQFVMVOVDZWGVPVMWMWN
      BVNVNVEVFVGVNABRVHVIVJAVQVKVGVL $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  $( The topological space built with a subspace topology.  (Contributed by FL,
     5-Jan-2009.)  (The proof was shortened by Mario Carneiro, 15-Dec-2013.) $)
  stoig $p |- ( ( J e. Top /\ A C_ U. J ) ->
   { <. ( Base ` ndx ) , A >. ,
       <. ( TopOpen ` ndx ) , ( subSp ` <. A , J >. ) >. } e. TopSp ) $=
    ( ctop wcel cuni wss wa cnx cbs cfv cop cts csubsp cpr ctps wceq cvv wb id
    uniexg ssexg syl2anc fvex eqid eltpsg sylancl stoig3 syldan stoig2 eqcomd
    mpbir2and ) BCDZABEZFZGZHIJAKHLJABKZMJZKNZODZUQCDZAUQEZPZUOAQDZUQQDUSUTVBGR
    UNUNUMQDVCULUNSBCTAUMQUAUBZUPMUCAQQUQURURUDUEUFULUNVCUTVDABQUGUHUOVAAABUIUJ
    UK $.
    $( [15-Dec-2013] $) $( [21-Oct-2012] $)

  ${
    $d w x y z B $.  $d w x y A $.  $d w x y z J $.
    $( Composition of subspaces.  (Contributed by Mario Carneiro,
       15-Dec-2013.) $)
    subspco $p |- ( ( J e. Top /\ A e. V /\ B e. W ) ->
                     ( subSp ` <. A , ( subSp ` <. B , J >. ) >. ) =
                       ( subSp ` <. ( A i^i B ) , J >. ) ) $=
      ( vx vy vz vw ctop wcel w3a cv cin wceq wrex cab cop csubsp cfv visset
      inex1 ineq1 in32 inass eqtri syl6eq abrexco simp1 simp3 subspval
      syl11anc opeq2d fveq2d stoig3 3adant2 eqeltrrd simp2 eqtrd cvv inex1g
      syl 3eqtr4a ) CJKZADKZBEKZLZFMZGMZANZOGHMIMZBNZOICPHQZPFQZVHVKABNZNZOICPF
      QZABCRSTZRZSTZVOCRSTZFGHICVLVJVPVKBIUAUBVIVLOVJVLANZVPVIVLAUCWBVKANBNVPVK
      BAUDVKABUEUFUGUHVGVTAVMRZSTZVNVGVSWCSVGVRVMAVGVDVFVRVMOVDVEVFUIZVDVEVFUJH
      IBCEUKULZUMUNVGVMJKVEWDVNOVGVRVMJWFVDVFVRJKVEBCEUOUPUQVDVEVFURZFGAVMDUKUL
      USVGVDVOUTKZWAVQOWEVGVEWHWGABDVAVBFIVOCUTUKULVC $.
      $( [15-Dec-2013] $)
  $}

  ${
    $d x y J $.  $d x y X $.  $d x y A $.  $d y V $.
    subspint.1 $e |- X = U. J $.
    $( When the subspace region is not a subset of the base of the topology,
       the resulting set is the same as the subspace restricted to the base.
       (Contributed by Mario Carneiro, 15-Dec-2013.) $)
    subspint $p |- ( ( J e. Top /\ A e. V ) ->
      ( subSp ` <. A , J >. ) = ( subSp ` <. ( A i^i X ) , J >. ) ) $=
      ( ctop wcel wa cop csubsp cfv cin wceq subspid adantr opeq2d fveq2d
      topopn subspco mpd3an3 eqtr3d ) BFGZACGZHZADBIJKZIZJKZABIZJKADLBIJKZUDUFU
      HJUDUEBAUBUEBMUCBDENOPQUBUCDBGZUGUIMUBUJUCBDEROADBCBSTUA $.
      $( [15-Dec-2013] $)
  $}

  ${
    $d x y J $.
    $( The subspace topology induced by the topology ` J ` on the empty set.
       (Contributed by FL, 22-Dec-2008.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    subsp0 $p |- ( J e. Top -> ( subSp ` <. (/) , J >. ) = { (/) } ) $=
      ( vx vy ctop wcel c0 cop csubsp cfv csn cv wceq cin wrex cvv wb 0ex
      elsubsp mpan2 in0 eqeq2i biimpi r19.23aivw cuni eqid topopn eqcomi ineq1
      eqeq2d rcla4ev sylancl eqeq1 rexbidv syl5ibrcom impbid2 bitrd elsn
      syl6bbr eqrdv ) ADEZBFAGHIZFJZUTBKZVAEZVCFLZVCVBEUTVDVCCKZFMZLZCANZVEUTFO
      EVDVIPQCVCFAORSUTVIVEVHVECAVHVEVGFVCVFTUAUBUCUTVIVEFVGLZCANZUTAUDZAEFVLFM
      ZLZVKAVLVLUEUFVMFVLTUGVJVNCVLAVFVLLVGVMFVFVLFUHUIUJUKVEVHVJCAVCFVGULUMUNU
      OUPBFUQURUS $.
      $( [15-Dec-2013] $) $( [22-Dec-2008] $)
  $}

  ${
    $d A x y $.  $d V x $.
    $( The only subspace topology induced by the topology ` { (/) } ` .
       (Contributed by FL, 5-Jan-2009.)  (Revised by Mario Carneiro,
       15-Dec-2013.) $)
    subspsn $p |- ( A e. V -> ( subSp ` <. A , { (/) } >. ) = { (/) } ) $=
      ( vx vy wcel c0 csn cop csubsp cfv cv wceq cin wrex ctop wb sn0top
      elsubsp mpan 0ex ineq1 eqeq2d rexsn incom in0 eqtri eqeq2i bitri syl6bb
      elsn syl6bbr eqrdv ) ABEZCAFGZHIJZUNUMCKZUOEZUPFLZUPUNEUMUQUPDKZAMZLZDUNN
      ZURUNOEUMUQVBPQDUPAUNBRSVBUPFAMZLZURVAVDDFTUSFLUTVCUPUSFAUAUBUCVCFUPVCAFM
      FFAUDAUEUFUGUHUICFUJUKUL $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d A x y $.  $d J x y $.  $d X x $.
    subspsn2.1 $e |- X = U. J $.
    $( The subspace topology induced by a singleton.  (Contributed by FL,
       5-Jan-2009.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    subspsn2 $p |- ( ( J e. Top /\ A e. X ) ->
      ( subSp ` <. { A } , J >. ) = { (/) , { A } } ) $=
      ( ctop wcel wa csn cop csubsp cfv cuni wceq c0 cpr cvv snex stoig3 mpan2
      adantr wss snssi eleq2s stoig2 sylan2 eqid topsn syl11anc ) BEFZACFZGAHZB
      IJKZEFZULLZUKMZULNUKOMUIUMUJUIUKPFUMAQUKBPRSTUJUIUKBLZUAZUOUQAUPCAUPUBDUC
      UKBUDUEAULUNUNUFUGUH $.
      $( [15-Dec-2013] $) $( [5-Jan-2009] $)
  $}

  ${
    $d o x A $.  $d o x J $.  $d o x S $.  $d o x X $.
    subcld.1 $e |- X = U. J $.
    $( A closed set of a subspace topology is a closed set of the original
       topology intersected with the subset.  (Contributed by FL,
       11-Jul-2009.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    subcld $p |- ( ( J e. Top /\ S C_ X ) -> ( A e. ( Clsd ` ( subSp ` <. S , J
    >. ) ) <-> E. x e. ( Clsd ` J ) A = ( x i^i S ) ) ) $=
      ( vo ctop wcel wss wa cop csubsp cfv ccld cuni cdif cv cin wceq wrex wb
      cvv uniexg sseq2i biimpi ssexg ancoms syl2an stoig3 syldan eqid iscld
      syl stoig2 sseq2d difeq1d eleq1d anbi12d sylan2b simpl elsubsp syl11anc
      anbi2d opncld adantlr adantr incom df-ss syl5eq ad2antlr ad2antrr difeq2
      cun c0 difindi difid uneq2i un0 3eqtri syl6eq adantl dfss4 3eqtr2rd
      difeq1i indif2 3eqtr2i ineq1 eqeq2d rcla4ev ex r19.23adva expimpd sylbid
      difin2 simpll cldopn elsubspr syl111anc eqeltrd inss2 jctil sseq1
      syl5ibrcom impbid 3bitrd ) DHIZCEJZKZBCDLMNZONIZBXJPZJZXLBQZXJIZKZBCJZCBQ
      ZXJIZKZBARZCSZTZADONZUAZXIXJHIZXKXPUBXGXHCUCIZYFXGDPZUCIZCYHJZYGXHDHUDXHY
      JEYHCFUEZUFYJYIYGCYHUCUGUHUIZCDUCUJUKBXJXLXLULUMUNXHXGYJXPXTUBYKXGYJKZXMX
      QXOXSYMXLCBCDUOZUPYMXNXRXJYMXLCBYNUQURUSUTXIXTYEXIXTXQXRGRZCSZTZGDUAZKYEX
      IXSYRXQXIXGYGXSYRUBXGXHVAYLGXRCDUCVBVCVDXIXQYRYEXIXQKZYQYEGDYSYODIZKZYQYE
      UUAYQKZEYOQZYDIZBUUCCSZTZYEUUAUUDYQXIYTUUDXQXGYTUUDXHYODEFVEVFVFVGUUBBECS
      ZYOQZUUEUUBUUHCYOQZCXRQZBUUBUUGCYOYSUUGCTZYTYQXHUUKXGXQXHUUGCESZCECVHZXHU
      ULCTCEVIUFVJVKVLUQYQUUJUUITUUAYQUUJCYPQZUUIXRYPCVMUUNUUICCQZVNUUIVOVNUUIC
      YOCVPUUOVOUUICVQZVRUUIVSVTWAWBYSUUJBTZYTYQXQUUQXIXQUUQBCWCUFWBVLWDUUHUULY
      OQCUUCSUUEUUGUULYOUUMWECEYOWFCUUCVHWGWAYCUUFAUUCYDYAUUCTYBUUEBYAUUCCWHWIW
      JVCWKWLWMWNXIYCXTAYDXIYAYDIZKZXTYCYBCJZCYBQZXJIZKUUSUVBUUTUUSUVAEYAQZCSZX
      JXIUVAUVDTUURXIUVACYAQZUVDUVAUVEUUOVNUVEVOVNUVECYACVPUUOVOUVEUUPVRUVEVSVT
      XHUVEUVDTXGCYAEWOWBVJVGUUSXGYGUVCDIZUVDXJIXGXHUURWPXIYGUURYLVGXGUURUVFXHY
      ADEFWQVFUVCCDUCWRWSWTYACXAXBYCXQUUTXSUVBBYBCXCYCXRUVAXJBYBCVMURUSXDWLXEXF
      $.
      $( [15-Dec-2013] $) $( [11-Jul-2009] $)
  $}

  ${
    $d o r s t x y z A $.  $d o r s t x y z C $.  $d o r s t x y z D $.
    $d o r s t x y z J $.  $d o r s t x y z K $.  $d o r s t x y z X $.
    $d o r s t x y z Y $.
    subtopmet.1 $e |- D = ( C |` ( Y X. Y ) ) $.
    subtopmet.2 $e |- X = dom dom C $.
    subtopmet.j $e |- J = ( MetOpen ` C ) $.
    subtopmet.k $e |- K = ( MetOpen ` D ) $.
    ${
      subtopmet.a $e |- A e. _V $.
      $( Lemma for ~ subtopmet . $)
      subtopmetlem $p |- ( ( C e. Met /\ Y C_ X ) -> ( A e. ( subSp ` <. Y , J
      >. ) -> A e. K ) ) $=
        ( vy vx vo vr vz cme wcel wss wa cv cin wceq wrex cdm cbl cfv crn wral
        cop csubsp w3a inss2 sseq1 mpbiri 3ad2ant3 basmetres 3ad2ant1 sseqtrd
        cc0 clt wbr co cr simpl1l simp2 adantr inss1 ssel2 sylan opni2
        syl111anc wi cxp cres metres syl5eqel 3adant3 syl3an1 eleqtrd simp3
        simprl eqid blelrn syl22anc blcntr simplrr wfun wfn wf metf ffun syl
        ad2antrr resss eqsstri funss mpsyl dmeqi dmres eqtri simp11r xpss12
        syl11anc fdm sseqtr4d df-ss sylib syl5eq df-fn sylanbrc a1i eqimss2
        sseld imp adantrr oprssoprv syl32anc simprr eqbrtrd wb simp11l sseldd
        dmss ax-mp sseli syl6eleqr ad2antrl simpll3 simplrl elbl2 mpbird ex
        dmxpid syl6sseq syl5eqss adantrd jcad elin syl6ibr elbl simpl13 eleq2d
        3imtr4d ssrdv eleq2 anbi12d rcla4ev syl12anc 3expia r19.23adv mpd
        r19.21aiva jca 3exp ctop cvv opntop sseq2i dmexg ssexg sylan2 sylanb
        ancoms elsubsp isopn ) BRSZGFTZUAZAMUBZGUCZUDZMDUEZACUFZUFZTZNUBZOUBZSZ
        UVSATZUAZOCUGUHZUIZUEZNAUJZUAZAGDUKULUHSZAESZUVJUVMUWGMDUVJUVKDSZUVMUWG
        UVJUWJUVMUMZUVQUWFUWKAGUVPUVMUVJAGTZUWJUVMUWLUVLGTUVKGUNAUVLGUOUPUQZUVJ
        UWJGUVPUDZUVMBCFGHIURUSZUTUWKUWENAUWKUVRASZUAZVAPUBZVBVCZUVRUWRBUGUHVDZ
        UVKTZUAZPVEUEZUWEUWQUVHUWJUVRUVKSZUXCUVHUVIUWJUVMUWPVFUWKUWJUWPUVJUWJUV
        MVGVHUWKAUVKTZUWPUXDUVMUVJUXEUWJUVMUXEUVLUVKTUVKGVIAUVLUVKUOUPUQAUVKUVR
        VJVKPUVKBUVRDJVLVMUWQUXBUWEPVEUWKUWPUWRVESZUXBUWEVNUWKUWPUXFUMZUXBUWEUX
        GUXBUAZUVRUWRUWCVDZUWDSZUVRUXISZUXIATZUWEUXHCRSZUVRUVPSZUXFUWSUXJUXGUXM
        UXBUWKUWPUXMUXFUVJUWJUXMUVMUVHUXMUVIUVHCBGGVOZVPZRHBGVQZVRVHUSUSVHZUXHU
        VRGUVPUXGUVRGSZUXBUWKUWLUWPUXFUXSUWMUWLUWPUXSUXFAGUVRVJVSVTZVHUXGUWNUXB
        UWKUWPUWNUXFUWOUSVHZWAZUXGUXFUXBUWKUWPUXFWBVHZUXGUWSUXAWCZCUVRUWRUVPUVP
        WDZWEWFUXHUXMUXNUXFUWSUXKUXRUYBUYCUYDCUVRUWRUVPUYEWGWFUXHQUXIAUXHQUBZUV
        PSZUVRUYFCVDZUWRVBVCZUAZUYFUVLSZUYFUXISZUYFASUXHUYJUYFUVKSZUYFGSZUAUYKU
        XHUYJUYMUYNUXHUYJUYMUXHUYJUAZUWTUVKUYFUXGUWSUXAUYJWHUYOUYFUWTSZUVRUYFBV
        DZUWRVBVCZUYOUYQUYHUWRVBUYOBWIZCUXOWJZCBTZUXSUYNUYQUYHUDUXGUYSUXBUYJUWK
        UWPUYSUXFUVJUWJUYSUVMUVHUYSUVIUVHFFVOZVEBWKZUYSBFIWLZVUBVEBWMWNVHUSUSWO
        ZUYOCWIZUVOUXOUDUYTVUAUYOUYSVUFCUXPBHBUXOWPWQZVUECBWRWSUYOUVOUXOBUFZUCZ
        UXOUVOUXPUFVUICUXPHWTBUXOXAXBZUYOUXOVUHTVUIUXOUDUYOUXOVUBVUHUYOUVIUVIUX
        OVUBTUXGUVIUXBUYJUVHUVIUWJUVMUWPUXFXCWOZVUKGFGFXDXEUXGVUHVUBUDZUXBUYJUW
        KUWPVULUXFUVJUWJVULUVMUVHVULUVIUVHVUCVULVUDVUBVEBXFWNVHUSUSWOXGUXOVUHXH
        XIXJCUXOXKXLVUAUYOVUGXMUXGUXSUXBUYJUXTWOZUXHUYGUYNUYIUXHUYGUYNUXHUVPGUY
        FUXHUWNUVPGTUYAUVPGXNWNXOXPXQUVRUYFGGBCXRXSUXHUYGUYIXTYAUYOUVHUVRFSUYFF
        SZUXFUWSUYPUYRYBUXGUVHUXBUYJUVHUVIUWJUVMUWPUXFYCWOUYOGFUVRVUKVUMYDUYGVU
        NUXHUYIUYGUYFVUHUFZFUVPVUOUYFUVOVUHTZUVPVUOTVUAVUPVUGCBYEYFUVOVUHYEYFYG
        IYHYIUWKUWPUXFUXBUYJYJUXGUWSUXAUYJYKUYFBUVRUWRFIYLXSYMYDYNUXHUYGUYNUYIU
        XHUVPGUYFUXHUVPVUIUFZGUVOVUIVUJWTUXHVUQUXOUFZGVUQVURTZUXHVUIUXOTVUSUXOV
        UHVIVUIUXOYEYFXMGYOYPYQXOYRYSUYFUVKGYTUUAUXHUXMUXNUXFUWSUYLUYJYBUXRUYBU
        YCUYDUYFCUVRUWRUVPUYEUUBWFUXHAUVLUYFUVJUWJUVMUWPUXFUXBUUCUUDUUEUUFUWBUX
        KUXLUAOUXIUWDUVSUXIUDUVTUXKUWAUXLUVSUXIUVRUUGUVSUXIAUOUUHUUIUUJYNUUKUUL
        UUMUUNUUOUUPUULUVJDUUQSZGUURSZUWHUVNYBUVHVUTUVIBDJUUSVHUVIUVHVVAUVIGVUO
        TZUVHVVAFVUOGIUUTUVHVVBVUOUURSZVVAUVHVUHUURSVVCBRUVAVUHUURUVAWNGVUOUURU
        VBUVCUVDUVEMAGDUURUVFXEUVJUXMUWIUWGYBUVJCUXPRHUVHUXPRSUVIUXQVHVRNOACEUV
        PUYEKUVGWNUUE $.
        $( [15-Dec-2013] $) $( [21-Aug-2009] $)
    $}

    $( Two alternate formulations of a subspace topology of a metric space
       topology.  (Contributed by Jeff Hankins, 21-Aug-2009.) $)
    subtopmet $p |- ( ( C e. Met /\ Y C_ X ) -> ( subSp ` <. Y , J >. ) = K )
        $=
      ( vt vy vr vx vz vs cme wcel wss wa cop csubsp cfv cv visset
      subtopmetlem cbl co wceq crp wrex cab cuni cin wex cc0 clt wbr cr cxp
      cres metres syl5eqel ad2antrr simprl simprr opni2 syl111anc simplll
      simplr cdm eqid opnss sylan ad2ant2r basmetres adantr sseqtr4d sseldd
      simprrl blcntr syl22anc elrp biimpri adantrr adantl simprrr eqidd opreq1
      sseq1d eqeq2d anbi12d opreq2 rcla42ev syl112anc oprex eleq2 eqeq1 anbi2d
      2rexbidv cla4ev syl11anc exp32 r19.23adv mpd expr eluniab syl6ibr sseld
      jcad elin biimpi wi simpll ad2antlr wb ad3antrrr simpr eleqtrd rpre
      ad2antll rpgt0 elbl adantlr wfun wfn wf metf ffun syl ffn 3syl resss
      eqsstri a1i oprssoprv syl32anc simpl adantll ssel2 rpregt0 elbl2
      syl31anc mpbid eqbrtrrd mpbir2and exp53 com14 r19.23advv imp com12
      19.23adv imp3a sylani syl5bi impbid eqrdv ctop cvv opntop id uniopn2
      uniexg eqeltrrd ssexg syl2anc w3a simp1ll 3adant3 3ad2ant2 blopn eleq1
      3ad2ant3 mpbird 3exp abssdv uniopn elsubspr eqeltrd ex ) AQRZFESZTZKFCUAU
      BUCZDUWCKUDZUWDRZUWEDRZUWEABCDEFGHIJKUEUFUWCUWGUWFUWCUWGTZUWELUDZMUDZBUGU
      CZUHZUWESZNUDZUWIUWJAUGUCZUHZUIZTZMUJUKLFUKZNULZUMZFUNZUWDUWHOUWEUXBUWHOU
      DZUWERZUXCUXBRZUWHUXDUXCUXARZUXCFRZTZUXEUWHUXDUXFUXGUWHUXDUXCUWNRZUWSTZNU
      OZUXFUWCUWGUXDUXKUWCUWGUXDTZTZUPPUDZUQURZUXCUXNUWKUHZUWESZTZPUSUKZUXKUXMB
      QRZUWGUXDUXSUWAUXTUWBUXLUWABAFFUTZVAZQGAFVBVCZVDUWCUWGUXDVEUWCUWGUXDVFZPU
      WEBUXCDJVGVHUXMUXRUXKPUSUXMUXNUSRZUXRUXKUXMUYEUXRTZTZUXCUXCUXNUWOUHZRZUWM
      UYHUWPUIZTZMUJUKLFUKZUXKUYGUWAUXCERZUYEUXOUYIUWAUWBUXLUYFVIUXMUYMUYFUXMFE
      UXCUWAUWBUXLVJUXMUWEFUXCUXMUWEBVKVKZFUWAUWGUWEUYNSZUWBUXDUWAUXTUWGUYOUYCU
      WEBDUYNUYNVLZJVMZVNVOUWCFUYNUIZUXLABEFGHVPZVQVRUYDVSZVSVQUXMUYEUXRVEUXMUY
      EUXOUXQVTAUXCUXNEHWAWBUYGUXGUXNUJRZUXQUYHUYHUIZUYLUXMUXGUYFUYTVQUYFVUAUXM
      UYEUXOVUAUXQVUAUYEUXOTUXNWCWDWEWFUXMUYEUXOUXQWGUYGUYHWHUYKUXQVUBTUXCUWJUW
      KUHZUWESZUYHUXCUWJUWOUHZUIZTLMUXCUXNFUJUWIUXCUIZUWMVUDUYJVUFVUGUWLVUCUWEU
      WIUXCUWJUWKWIWJVUGUWPVUEUYHUWIUXCUWJUWOWIWKWLUWJUXNUIZVUDUXQVUFVUBVUHVUCU
      XPUWEUWJUXNUXCUWKWMWJVUHVUEUYHUYHUWJUXNUXCUWOWMWKWLWNWOUXJUYIUYLTNUYHUXCU
      XNUWOWPUWNUYHUIZUXIUYIUWSUYLUWNUYHUXCWQVUIUWRUYKLMFUJVUIUWQUYJUWMUWNUYHUW
      PWRWSWTWLXAXBXCXDXEXFUWSNUXCXGZXHUWHUWEFUXCUWHUWEUYNFUWCUXTUWGUYOUWAUXTUW
      BUYCVQUYQVNUWCUYRUWGUYSVQVRXIXJUXCUXAFXKZXHUXEUXHUWHUXDVUKUXFUWHUXKUXGUXD
      UXFUXKVUJXLUWHUXKUXGUXDUWHUXJUXGUXDXMZNUXJUWHVULUXIUWSUWHVULXMZUXIUWRVUML
      MFUJUWHUWIFRZUWJUJRZTZUWRUXIVULUWHVUPUWRUXIUXGUXDUWHVUPTZUWRUXITZTZUXGTZU
      WLUWEUXCVURUWMVUQUXGUWMUWQUXIXNXOVUTUXCUWLRZUXCUYNRZUWIUXCBUHZUWJUQURZVUT
      UXTUWIUYNRZUWJUSRZUPUWJUQURZVVAVVBVVDTXPVUQUXTVURUXGUWAUXTUWBUWGVUPUYCXQV
      DVUQVVEVURUXGUWCVUNVVEUWGVUOUWCVUNTUWIFUYNUWCVUNXRUWCUYRVUNUYSVQXSVOVDVUQ
      VVFVURUXGVUOVVFUWHVUNUWJXTZYAVDVUQVVGVURUXGVUOVVGUWHVUNUWJYBZYAVDUXCBUWIU
      WJUYNUYPYCWBVUQUXGVVBVURUWHUXGVVBVUPUWCUXGVVBUWGUWCUXGTUXCFUYNUWCUXGXRUWC
      UYRUXGUYSVQXSYDYDYDVUTUWIUXCAUHZVVCUWJUQVUTAYEZBUYNUYNUTZYFZBASZVVEVVBVVJ
      VVCUIVUQVVKVURUXGUWAVVKUWBUWGVUPUWAEEUTZUSAYGVVKAEHYHVVOUSAYIYJXQVDVUQVVM
      VURUXGUWAVVMUWBUWGVUPUWAUXTVVLUSBYGVVMUYCBUYNUYPYHVVLUSBYKYLXQVDVVNVUTBUY
      BAGAUYAYMYNYOVUTUWIFUYNVUQVUNVURUXGUWHVUNVUOVEVDVUQUYRVURUXGUWCUYRUWGVUPU
      YSVDVDZXSVUTUXCFUYNVUSUXGXRVVPXSUWIUXCUYNUYNABYPYQVUTUXCUWPRZVVJUWJUQURZV
      URVVQVUQUXGUWQUXIVVQUWMUWQUXITUXCUWNUWPUWQUXIXRUWQUXIYRXSYSXOVUTUWAUWIERZ
      UYMVVFVVGTZVVQVVRXPVUQUWAVURUXGUWAUWBUWGVUPVIVDVUQVVSVURUXGUWCVUNVVSUWGVU
      OUWBVUNVVSUWAFEUWIYTZYSVOVDVUQUXGUYMVURUWHUXGUYMVUPUWCUXGUYMUWGUWBUXGUYMU
      WAFEUXCYTYSYDYDYDVUQVVTVURUXGVUOVVTUWHVUNUWJUUAYAVDUXCAUWIUWJEHUUBUUCUUDU
      UEUUFVSUUGUUHUUIUUJUUKUULUUMUUNUUOUUPUUQUWHCUURRZFUUSRZUXACRZUXBUWDRUWAVW
      BUWBUWGACIUUTZVDZUWCVWCUWGUWBUWBEUUSRVWCUWAUWBUVAUWACUMZEUUSACEHIUVBUWAVW
      BVWGUUSRVWECUURUVCYJUVDFEUUSUVEUVFVQUWHVWBUWTCSVWDVWFUWHUWSNCUWHUWRUWNCRZ
      LMFUJUWHVUPUWRVWHUWHVUPUWRUVGZVWHUWPCRZVWIUWAVVSVVFVVGVWJUWAUWBUWGVUPUWRU
      VHUWHVUPVVSUWRUWHVUNVVSVUOUWHUWBVUNVVSUWAUWBUWGVJVWAVNWEUVIVUPUWHVVFUWRVU
      OVVFVUNVVHWFUVJVUPUWHVVGUWRVUOVVGVUNVVIWFUVJAUWIUWJCEHIUVKWBUWRUWHVWHVWJX
      PZVUPUWQVWKUWMUWNUWPCUVLWFUVMUVNUVOUUIUVPUWTCUVQXBUXAFCUUSUVRVHUVSUVTUUPU
      UQ $.
      $( [15-Dec-2013] $) $( [19-Aug-2009] $)
  $}


