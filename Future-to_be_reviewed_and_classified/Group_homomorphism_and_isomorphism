$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Group homomorphism and isomorphism
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c GrpHom $.

  $( Extend class notation to include the class of group homomorphisms. $)
  cghom $a class GrpHom $.

  ${
    $d f g h s x y $.
    $( Define the set of group homomorphisms from ` g ` to ` h ` .
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    df-ghom $a |- GrpHom = { <. <. g , h >. , s >. | ( ( g e. Grp /\ h e. Grp )
        /\ s = { f | ( f : ran g --> ran h /\ A. x e. ran g A. y e. ran g
                     ( ( f ` x ) h ( f ` y ) ) = ( f ` ( x g y ) ) ) } ) } $.
  $}

  $c GrpIso $.

  $( Extend class notation to include the class of group isomorphisms. $)
  cgiso $a class GrpIso $.

  ${
    $d f g h s $.
    $( Define the set of group isomorphisms from ` g ` to ` h ` .  (Contributed
       by Paul Chapman, 25-Feb-2008.) $)
    df-giso $a |- GrpIso = { <. <. g , h >. , s >. | ( ( g e. Grp /\ h e. Grp )
        /\ s = { f e. ( g GrpHom h ) | f : ran g -1-1-onto-> ran h } ) } $.
  $}

  ${
    $d F f x y $.  $d G f g h x y z $.  $d H f g h x y z $.  $d S h z $.
    elghomlem1.1 $e |- S = { f | ( f : ran G --> ran H /\ A. x e. ran G
      A. y e. ran G ( ( f ` x ) H ( f ` y ) ) = ( f ` ( x G y ) ) ) } $.
    $( Lemma for ~ elghom . $)
    elghomlem1 $p |- ( ( G e. Grp /\ H e. Grp ) -> ( G GrpHom H ) = S ) $=
      ( vg vh vz cgr wcel cvv cghom co wceq crn cv cfv wral fabexg rnexg
      syl2an wf wa cab rneq feq2d opreq fveq2d eqeq2d raleqbidv anbi12d abbidv
      wb feq3 syl eqeq1d 2ralbidv syl6eqr df-ghom oprabval2g mpd3an3 ) EKLZFKLZ
      CMLZEFNOCPEQZMLFQZMLVFVDVEARZDRZSZBRZVJSZFOZVIVLEOZVJSZPZBVGTAVGTZDVGVHMM
      CGUAEKUBFKUBUCHIJEFKKHRZQZIRZQZVJUDZVKVMWAOZVIVLVSOZVJSZPZBVTTZAVTTZUEZDU
      FCNVGWBVJUDZWDVPPZBVGTZAVGTZUEZDUFZMVSEPZWJWODWQWCWKWIWNWQVTVGWBVJVSEUGZU
      HWQWHWMAVTVGWRWQWGWLBVTVGWRWQWFVPWDWQWEVOVJVIVLVSEUIUJUKULULUMUNWAFPZWPVG
      VHVJUDZVRUEZDUFCWSWOXADWSWKWTWNVRWSWBVHPWKWTUOWAFUGWBVHVGVJUPUQWSWLVQABVG
      VGWSWDVNVPVKVMWAFUIURUSUMUNGUTABDHIJVAVBVC $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)

    $( Lemma for ~ elghom . $)
    elghomlem2 $p |- ( ( G e. Grp /\ H e. Grp ) -> ( F e. ( G GrpHom H ) <->
        ( F : ran G --> ran H /\ A. x e. ran G A. y e. ran G
          ( ( F ` x ) H ( F ` y ) ) = ( F ` ( x G y ) ) ) ) ) $=
      ( cgr wcel wa cghom co crn wf cv cfv wceq wral elghomlem1 eleq2d wb cvv
      elisset feq1 fveq1 opreq12d eqeq12d 2ralbidv anbi12d elab2g biimpd mpcom
      wi rnexg fex expcom syl adantrd biimprd syli impbid2 adantr bitrd ) FIJZG
      IJZKZEFGLMZJECJZFNZGNZEOZAPZEQZBPZEQZGMZVMVOFMZEQZRZBVJSAVJSZKZVGVHCEABCD
      FGHTUAVEVIWBUBVFVEVIWBEUCJZVIWBECUDWCVIWBVJVKDPZOZVMWDQZVOWDQZGMZVRWDQZRZ
      BVJSAVJSZKWBDECUCWDERZWEVLWKWAVJVKWDEUEWLWJVTABVJVJWLWHVQWIVSWLWFVNWGVPGV
      MWDEUFVOWDEUFUGVRWDEUFUHUIUJHUKZULUMWBVEWCVIVEVLWCWAVEVJUCJZVLWCUNFIUOVLW
      NWCVJVKUCEUPUQURUSWCVIWBWMUTVAVBVCVD $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)
  $}

  ${
    $d F f x y $.  $d G f x y $.  $d H f x y $.  $d X x y $.
    elghom.1 $e |- X = ran G $.
    elghom.2 $e |- W = ran H $.
    $( Membership in the set of group homomorphisms from ` G ` to ` H ` .
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    elghom $p |- ( ( G e. Grp /\ H e. Grp ) ->
      ( F e. ( G GrpHom H ) <-> ( F : X --> W /\ A. x e. X A. y e. X
        ( ( F ` x ) H ( F ` y ) ) = ( F ` ( x G y ) ) ) ) ) $=
      ( vf cgr wcel wa cghom co crn wf cv cfv wceq wral cab eqid elghomlem2 wb
      feq23 mp2an raleq ax-mp ralbii bitri anbi12i syl6bbr ) DKLEKLMCDENOLDPZEP
      ZCQZARZCSBRZCSEOUQURDOZCSTZBUNUAZAUNUAZMGFCQZUTBGUAZAGUAZMABUNUOJRZQUQVFS
      URVFSEOUSVFSTBUNUAAUNUAMJUBZJCDEVGUCUDVCUPVEVBGUNTZFUOTVCUPUEHIGFUNUOCUFU
      GVEVAAGUAZVBVDVAAGVHVDVAUEHUTBGUNUHUIUJVHVIVBUEHVAAGUNUHUIUKULUM $.
      $( [1-Dec-2010] $) $( [25-Feb-2008] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d F x y $.  $d G x y $.  $d H x y $.  $d X x y $.
    ghomlin.1 $e |- X = ran G $.
    $( Linearity of a group homomorphism.  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    ghomlin $p |- ( ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) /\
                      ( A e. X /\ B e. X ) ) ->
                    ( ( F ` A ) H ( F ` B ) ) = ( F ` ( A G B ) ) ) $=
      ( vx vy cgr wcel cghom co w3a cv cfv wceq wral wa crn wf eqid elghom
      biimp3a simprd fveq2 opreq1d opreq1 fveq2d eqeq12d opreq2d opreq2
      rcla42v mpan9 ) DJKZEJKZCDELMKZNZHOZCPZIOZCPZEMZUSVADMZCPZQZIFRHFRZAFKBFK
      SACPZBCPZEMZABDMZCPZQZURFETZCUAZVGUOUPUQVOVGSHICDEVNFGVNUBUCUDUEVFVMVHVBE
      MZAVADMZCPZQHIABFFUSAQZVCVPVEVRVSUTVHVBEUSACUFUGVSVDVQCUSAVADUHUIUJVABQZV
      PVJVRVLVTVBVIVHEVABCUFUKVTVQVKCVABADULUIUJUMUN $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.
    ghomid.1 $e |- U = ( Id ` G ) $.
    ghomid.2 $e |- T = ( Id ` H ) $.
    $( A group homomorphism maps identity element to identity element.
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    ghomid $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ->
                   ( F ` U ) = T ) $=
      ( vx vy cgr wcel cghom co w3a cfv wceq crn wa eqid grpidcl 3ad2ant1 jca
      ghomlin mpdan grplid fveq2d eqtrd wb wf cv wral elghom biimp3a simplld
      ffvelrn syl11anc wi grpid ex 3ad2ant2 mpd mpbird ) DJKZEJKZCDELMKZNZBCOZA
      PZVGVGEMZVGPZVFVIBBDMZCOZVGVFBDQZKZVNRVIVLPVFVNVNVCVDVNVEBDVMVMSZFTZUAZVQ
      UBBBCDEVMVOUCUDVCVDVLVGPVEVCVKBCVCVNVKBPVPBBDVMVOFUEUDUFUAUGVFVGEQZKZVHVJ
      UHZVFVMVRCUIZVNVSVFWAHUJZCOIUJZCOEMWBWCDMCOPIVMUKHVMUKZVCVDVEWAWDRHICDEVR
      VMVOVRSZULUMUNVQVMVRBCUOUPVDVCVSVTUQVEVDVSVTVGAEVRWEGURUSUTVAVB $.
      $( [3-Mar-2008] $)
  $}

