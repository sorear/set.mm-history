$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Groups and related structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Magma $.

  $( Extend class notation with the class of all magmas. $)
  cmagm $a class Magma $.

  ${
    $d g t $.
    $( A magma is a binary internal operation. $)
    df-mgm $a |- Magma = { g | E. t g : ( t X. t ) --> t } $.
  $}

  ${
    $d G g t $.  $d X t $.
    ismgm.1 $e |- X = dom dom G $.
    $( The predicate "is a magma".  (Contributed by FL, 2-Nov-2009.) $)
    ismgm $p |- ( G e. A -> ( G e. Magma <-> G : ( X X. X ) --> X ) ) $=
      ( vt vg wcel cmagm cv cdm wceq cxp wf wa wex feq1 exbidv df-mgm elab2g
      c0 wi f00 dmeq dm0 dmeqi eqtri syl6req syl adantr sylbi wb xpeq12 anidms
      feq23 mpancom eqeq1 imbi12d mpbiri wn fdm wne df-ne dmxp sylbir eqeq1d
      biimpcd eqcoms com12 pm2.61i pm4.71ri exbii syl6bb cvv dmexg eqcomi
      xpeq12i mp2an ceqsexgv bitrd ) BAGZBHGZEIZBJZJZKZWBWBLZWBBMZNZEOZCCLZCBMZ
      VTWAWGEOZWIWFWBFIZMZEOWLFBHAWMBKWNWGEWFWBWMBPQEFRSWGWHEWGWEWBTKZWGWEUAZWO
      WPTTLZTBMZTWDKZUAWRBTKZWQTKZNWSWQBUBWTWSXAWTWCTJZKZWSBTUCXCWDXBJZTWCXBUCX
      DXBTXBTUDUEUDUFUGUHUIUJWOWGWRWEWSWFWQKZWOWGWRUKWOXEWBTWBTULUMWFWBWQTBUNUO
      WBTWDUPUQURWGWOUSZWEWGWCWFKZXFWEUAZWFWBBUTXGWDWFJZKXHWCWFUCXHXIWDXFXIWDKW
      EXFXIWBWDXFWBTVAXIWBKWBTVBWBWBVCVDVEVFVGUHUHVHVIVJVKVLVTWDVMGZWIWKUKVTWCV
      MGXJBAVNWCVMVNUHWGWKEWDVMWEWGWDWDLZWDBMZWKWFXKKZWEWGXLUKWEXMWBWDWBWDULUMW
      FWBXKWDBUNUOXKWJKWDCKXLWKUKWDCWDCCWDDVOZXNVPXNXKWDWJCBUNVQVLVRUHVS $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

  ${
    clmgm.1 $e |- X = dom dom G $.
    $( Closure of a magma.  (Contributed by FL, 14-Sep-2010.) $)
    clmgm $p |- ( ( G e. Magma /\ A e. X /\ B e. X ) -> ( A G B ) e. X ) $=
      ( cmagm wcel co wi cxp wf ismgm foprrn 3exp syl6bi pm2.43i 3imp ) CFGZADG
      ZBDGZABCHDGZRSTUAIIZRRDDJDCKZUBFCDELUCSTUAABDDDCMNOPQ $.
      $( [20-Sep-2010] $) $( [14-Sep-2010] $)
  $}

  ${
    $d G u x z $.  $d G u y z $.  $d X u x z $.  $d X u y z $.
    opidon.1 $e |- X = dom dom G $.
    $( An operation with a left and right identity element is onto.
       (Contributed by FL, 2-Nov-2009.) $)
    opidon $p |- ( G e. ( Magma i^i ExId ) ->
      G : ( X X. X ) -onto-> X ) $=
      ( vz vu vy vx cmagm cexid cin wcel cxp wf cv co wceq wrex wral wa wfo
      inss1 sseli ismgm ibi syl isexid biimpd inss2 sylc opreq2 id eqeq12d
      rcla4v eqeq1d eqcom syl5bb rcla4ev ex syld pm3.27 r19.20si syl5
      r19.22sdv impcom r19.21aiva jca fooprv sylibr ) AHIJZKZBBLZBAMZDNZENZFNZA
      OZPZFBQZEBQZDBRZSVKBATVJVLVTVJAHKZVLVIHAHIUAUBWAVLHABCUCUDUEVJGNZVNAOWBPZ
      VNWBAOZWBPZSZGBRZEBQZVTAIKZWIWHVJWIWIWHEGIABCUFUGVIIAHIUHUBZWJUIWHVSDBVMB
      KZWHVSWKWGVREBWKWEGBRZVRWGWKWLVNVMAOZVMPZVRWEWNGVMBWBVMPZWDWMWBVMWBVMVNAU
      JWOUKULUMWKWNVRVQWNFVMBVOVMPZVPVMPWNVQWPVPWMVMVOVMVNAUJUNVMVPUOUPUQURUSWF
      WEGBWCWEUTVAVBVCVDVEUEVFEFDBBBAVGVH $.
      $( [14-Dec-2009] $) $( [2-Nov-2009] $)
  $}

  $( Range of an operation with a left and right identity element.
     (Contributed by FL, 2-Nov-2009.) $)
  rngopid $p |- ( G e. ( Magma i^i ExId ) -> ran G = dom dom G ) $=
    ( cmagm cexid cin wcel cdm cxp wfo crn wceq eqid opidon forn syl ) ABCDEAFF
    ZOGZOAHAIOJAOOKLPOAMN $.
    $( [14-Dec-2009] $) $( [2-Nov-2009] $)

  ${
    opidon2.1 $e |- X = ran G $.
    $( An operation with a left and right identity element is onto.
       (Contributed by FL, 2-Nov-2009.) $)
    opidon2 $p |- ( G e. ( Magma i^i ExId ) ->
      G : ( X X. X ) -onto-> X ) $=
      ( cmagm cexid cin wcel cdm cxp wfo eqid opidon wceq crn forn syl5req wb
      xpeq12 anidms foeq2 syl foeq3 bitrd biimpd mpcom ) ADEFGAHHZUFIZUFAJZBBIZ
      BAJZAUFUFKLUFBMZUHUJUHANUFBUGUFAOCPUKUHUJUKUHUIUFAJZUJUKUGUIMZUHULQUKUMUF
      BUFBRSUGUIUFATUAUFBUIAUBUCUDUEUA $.
      $( [14-Dec-2009] $) $( [2-Nov-2009] $)
  $}

  ${
    $d G u y $.  $d X u y $.
    isexid2.1 $e |- X = ran G $.
    $( If ` G e. ( Magma i^i ExId ) ` then it has a left and right identity
       element that belongs to the range of the operation.  (Contributed by FL,
       12-Dec-2009.) $)
    isexid2 $p |- ( G e. ( Magma i^i ExId ) -> E. u e. X A. y e. X
      ( ( y G u ) = y /\ ( u G y ) = y ) ) $=
      ( crn wceq cmagm cexid cin wcel cv co wa wral wrex wi cdm rngopid eqeq2
      eleq2 raleq1 anbi12d rexbidv2 imbi12d elin eqid isexid ibi a1d adantl
      sylbi syl5bir mpcom com12 id raleq1d sylibrd ax-mp ) DCFZGZCHIJKZALZBLZCM
      VCGVDVCCMVCGNZADOZBDPZQEVAVBVEAUTOZBUTPZVGVBVAVIUTCRRZGZVBVAVIQZCSVKVLDVJ
      GZVEAVJOZBVJPZQZVBVKVAVMVIVOUTVJDTVKVHVNBUTVJVKVDUTKZVDVJKVHVNUTVJVDUAVEA
      UTVJUBUCUDUEVBCHKZCIKZNVPCHIUFVSVPVRVSVOVMVSVOBAICVJVJUGUHUIUJUKULUMUNUOV
      AVFVHBDUTVAVDDKVQVFVHDUTVDUAVAVEADUTVAUPUQUCUDURUS $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
  $}

  ${
    $d G u w x z $.  $d X u w x z $.
    exidu1.1 $e |- X = ran G $.
    $( Unicity of the left and right identity element of a magma when it
       exists.  (Contributed by FL, 12-Dec-2009.) $)
    exidu1 $p |- ( G e. ( Magma i^i ExId ) ->
      E! u e. X A. x e. X ( ( u G x ) = x /\ ( x G u ) = x ) ) $=
      ( vw vz cmagm cexid cin wcel cv co wceq wa wral wi wrex wreu isexid2
      opreq2 id eqeq12d opreq1 anbi12d ancom syl5bb cbvralv biimpi adantl
      rcla4va eqtr3 syl expcom imp adantr pm3.26d ex ad2antrr pm3.27 r19.20si
      syl5com 3eqtr3d r19.21aiva jca r19.22dva mpd eqeq1d ralbidv reu8 sylibr
      ) CHIJKZBLZALZCMZVNNZVNVMCMZVNNZOZADPZFLZVNCMZVNNZVNWACMZVNNZOZADPZVMWANZ
      QZFDPZOZBDRZVTBDSVLGLZVMCMZWMNZVMWMCMZWMNZOZGDPZBDRWLGBCDETVLWSWKBDVLVMDK
      ZOZWSWKXAWSOZVTWJWSVTXAWSVTWRVSGADWMVNNZWQWOOVSWRXCWQVPWOVRXCWPVOWMVNWMVN
      VMCUAXCUBZUCXCWNVQWMVNWMVNVMCUDXDUCUEWOWQUFUGUHUIUJXBWIFDXBWADKZOZWGWHXFW
      GOWAVMCMZVMWACMZVMWAXFXGXHNZWGXBXEXIWSXEXIQXAXEWSXIXEWSOXGWANZXHWANZOZXIW
      RXLGWADWMWANZWOXJWQXKXMWNXGWMWAWMWAVMCUDXMUBZUCXMWPXHWMWAWMWAVMCUAXNUCZUE
      UKXGXHWAULUMUNUJUOUPXFWGXGVMNZXAWGXPQZWSXEWTXQVLWTWGXPWTWGOXPXHVMNZWFXPXR
      OAVMDVNVMNZWCXPWEXRXSWBXGVNVMVNVMWACUAXSUBZUCXSWDXHVNVMVNVMWACUDXTUCUEUKU
      QURUJUSUOXFXKWGXBXEXKWSXEXKQXAXEWQGDPZXKWSXEYAXKWQXKGWADXOUKURWRWQGDWOWQU
      TVAVBUJUOUPVCURVDVEURVFVGVTWGBFDWHVSWFADWHVPWCVRWEWHVOWBVNVMWAVNCUDVHWHVQ
      WDVNVMWAVNCUAVHUEVIVJVK $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
  $}

  ${
    $d G g u x y $.  $d X g u x y $.
    idrval.1 $e |- X = ran G $.
    idrval.2 $e |- U = ( Id ` G ) $.
    $( The value of the identity element.  (Contributed by FL, 12-Dec-2009.) $)
    idrval $p |- ( G e. A ->
                   U = U. { u e. X | A. x e. X ( ( u G x ) = x
      /\ ( x G u ) = x ) } ) $=
      ( vg vy wcel cgi cfv cv co wceq wa wral crab cuni cvv crn rneq syl6eqr
      rabeq syl opreq eqeq1d anbi12d raleqbidv rabbisdv eqtrd unieqd copab
      cres wrel relopab resid df-gid syl6reqr ax-mp resopab eqtri fvopab4g
      elisset rnexg syl5eqel rabexg uniexg 3syl sylanc syl5eq ) ECKZELMZBNZANZE
      OZVPPZVPVOEOZVPPZQZAFRZBFSZTZDEUAKWDUAKZVNWDPVMIJEVOVPINZOZVPPZVPVOWFOZVP
      PZQZAWFUBZRZBWLSZTZWDUAUALWFEPZWNWCWPWNWMBFSZWCWPWLFPWNWQPWPWLEUBZFWFEUCG
      UDZWMBWLFUEUFWPWMWBBFWPWKWAAWLFWSWPWHVRWJVTWPWGVQVPVOVPWFEUGUHWPWIVSVPVPV
      OWFEUGUHUIUJUKULUMLJNWOPZIJUNZUAUOZWFUAKWTQIJUNXAUPZLXBPWTIJUQXCXBXALXAUR
      AJBIUSUTVAWTIJUAVBVCVDECVEVMFUAKWCUAKWEVMWRUAFECVFGVGWBBFUAVHWCUAVIVJVKHV
      L $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
  $}

  ${
    $d G u x $.  $d X u x $.
    iorlid.1 $e |- X = ran G $.
    iorlid.2 $e |- U = ( Id ` G ) $.
    $( A magma right and left identity belongs to the underlying set of the
       operation.  (Contributed by FL, 12-Dec-2009.) $)
    iorlid $p |- ( G e. ( Magma i^i ExId ) -> U e. X ) $=
      ( vu vx cmagm cexid cin wcel cv co wceq wa wral crab cuni idrval wreu
      exidu1 reucl syl eqeltrd ) BHIJZKZAFLZGLZBMUHNUHUGBMUHNOGCPZFCQRZCGFUEABC
      DESUFUIFCTUJCKGFBCDUAUIFCUBUCUD $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
  $}


  ${
    $d A x $.  $d G u x $.  $d U u x $.  $d X u x $.
    cmpidelt.1 $e |- X = ran G $.
    cmpidelt.2 $e |- U = ( Id ` G ) $.
    $( A magma right and left identity element keeps the other elements
       unchanged.  (Contributed by FL, 12-Dec-2009.) $)
    cmpidelt $p |- ( ( G e. ( Magma i^i ExId ) /\ A e. X ) ->
      ( ( U G A ) = A /\ ( A G U ) = A ) ) $=
      ( vx vu cv co wceq wa wral wcel cmagm cexid cin opreq2 id eqeq12d opreq1
      anbi12d rcla4cva crab cuni idrval eqcomd wreu wb eqeq1d ralbidv reuuni2
      iorlid exidu1 sylanc mpbird sylan ) BGIZCJZURKZURBCJZURKZLZGDMZADNBACJZAK
      ZABCJZAKZLZCOPQZNZVCVIGADURAKZUTVFVBVHVLUSVEURAURABCRVLSZTVLVAVGURAURABCU
      AVMTUBUCVKVDHIZURCJZURKZURVNCJZURKZLZGDMZHDUDUEZBKZVKBWAGHVJBCDEFUFUGBDNV
      THDUHVDWBUIVKVTVDHDBVNBKZVSVCGDWCVPUTVRVBWCVOUSURVNBURCUAUJWCVQVAURVNBURC
      RUJUBUKULBCDEFUMGHCDEUNUOUPUQ $.
      $( [14-Dec-2009] $) $( [12-Dec-2009] $)
  $}

  $c SemiGrp $.

  $( Extend class notation with the class of all semi-groups. $)
  csem $a class SemiGrp $.

  $( A semi-group is an associative magma. $)
  df-sgr $a |- SemiGrp = ( Magma i^i Ass ) $.

  $( A semi-group is a magma.  (Contributed by FL, 2-Nov-2009.) $)
  smgrpismgm $p |- ( G e. SemiGrp -> G e. Magma ) $=
    ( csem wcel cmagm cass cin df-sgr eleq2i elin pm3.26bi sylbi ) ABCADEFZCZAD
    CZBLAGHMNAECADEIJK $.
    $( [3-Nov-2009] $) $( [2-Nov-2009] $)

  $( A semi-group is associative.  (Contributed by FL, 2-Nov-2009.) $)
  smgrpisass $p |- ( G e. SemiGrp -> G e. Ass ) $=
    ( csem wcel cmagm cass cin df-sgr eleq2i elin pm3.27bi sylbi ) ABCADEFZCZAE
    CZBLAGHMADCNADEIJK $.
    $( [3-Nov-2009] $) $( [2-Nov-2009] $)

  ${
    $d G x y z $.  $d X x y z $.
    issmgrp.1 $e |- X = dom dom G $.
    $( The predicate "is a semi-group".  (Contributed by FL, 2-Nov-2009.) $)
    issmgrp $p |- ( G e. A -> ( G e. SemiGrp <-> ( G : ( X X. X ) --> X /\
     A. x e. X A. y e. X A. z e. X ( ( x G y ) G z ) = ( x G ( y G z ) ) ) ) )
        $=
      ( wcel cmagm cass cin cxp wf cv co wceq wral wa csem ismgm isass anbi12d
      elin syl5bb df-sgr eleq2i ) EDHZEIJKZHZFFLFEMZANZBNZEOCNZEOUKULUMEOEOPCFQ
      BFQAFQZRZESHUGEIHZEJHZRUOUIUGUPUJUQUNDEFGTABCDEFGUAUBEIJUCUDSUHEUEUFUD $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

  ${
    $d G x y z $.  $d X x y z $.
    smgrpmgm.1 $e |- X = dom dom G $.
    $( A semi-group is a magma.  (Contributed by FL, 2-Nov-2009.) $)
    smgrpmgm $p |- ( G e. SemiGrp -> G : ( X X. X ) --> X ) $=
      ( vx vy vz csem wcel cxp wf cv co wceq wral wa issmgrp pm3.26 syl6bi
      pm2.43i ) AGHZBBIBAJZTTUADKZEKZALFKZALUBUCUDALALMFBNEBNDBNZOUADEFGABCPUAU
      EQRS $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

  ${
    $d G x y z $.  $d X x y z $.
    smgrpass.1 $e |- X = dom dom G $.
    $( A semi-group is associative.  (Contributed by FL, 2-Nov-2009.) $)
    smgrpass $p |- ( G e. SemiGrp ->
      A. x e. X A. y e. X A. z e. X ( ( x G y ) G z ) = ( x G ( y G z ) ) ) $=
      ( csem wcel cv co wceq wral cxp wf wa issmgrp pm3.27 syl6bi pm2.43i ) DGH
      ZAIZBIZDJCIZDJUAUBUCDJDJKCELBELAELZTTEEMEDNZUDOUDABCGDEFPUEUDQRS $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

  $c Mnd $.

  $( Extend class notation with the class of all monoids. $)
  cmnd $a class Mnd $.

  $( A monoid is a semi-group with an identity element. $)
  df-mnd $a |- Mnd = ( SemiGrp i^i ExId ) $.

  $( A monoid is a semi-group.  (Contributed by FL, 2-Nov-2009.) $)
  mndissmgrp $p |- ( G e. Mnd -> G e. SemiGrp ) $=
    ( cmnd wcel csem cexid cin df-mnd eleq2i elin pm3.26bi sylbi ) ABCADEFZCZAD
    CZBLAGHMNAECADEIJK $.
    $( [3-Nov-2009] $) $( [2-Nov-2009] $)

  $( A monoid has an identity element.  (Contributed by FL, 2-Nov-2009.) $)
  mndisexid $p |- ( G e. Mnd -> G e. ExId ) $=
    ( cmnd wcel csem cexid cin df-mnd eleq2i elin pm3.27bi sylbi ) ABCADEFZCZAE
    CZBLAGHMADCNADEIJK $.
    $( [3-Nov-2009] $) $( [2-Nov-2009] $)

  $( A monoid is a magma.  (Contributed by FL, 2-Nov-2009.) $)
  mndismgm $p |- ( G e. Mnd -> G e. Magma ) $=
    ( cmnd wcel csem cmagm mndissmgrp smgrpismgm syl ) ABCADCAECAFAGH $.
    $( [3-Nov-2009] $) $( [2-Nov-2009] $)

  $( A monoid is a magma with an identity element.  (Contributed by FL,
     18-Feb-2010.) $)
  mndmgmid $p |- ( G e. Mnd -> G e. ( Magma i^i ExId ) ) $=
    ( cmnd wcel cmagm cexid wa cin mndismgm mndisexid jca elin sylibr ) ABCZADC
    ZAECZFADEGCMNOAHAIJADEKL $.
    $( [18-Feb-2010] $) $( [18-Feb-2010] $)

  ${
    $d G x y $.
    ismond.1 $e |- X = dom dom G $.
    $( The predicate "is a monoid".  (Contributed by FL, 2-Nov-2009.) $)
    ismnd $p |- ( G e. A -> ( G e. Mnd
     <-> ( G e. SemiGrp /\ E. x e. X A. y e. X
      ( ( y G x ) = y /\ ( x G y ) = y ) ) ) ) $=
      ( wcel csem cexid cin cv co wceq wa wral wrex cmnd isexid anbi2d elin
      syl5bb df-mnd eleq2i ) DCGZDHIJZGZDHGZBKZAKZDLUHMUIUHDLUHMNBEOAEPZNZDQGUD
      UGDIGZNUKUFUDULUJUGABCDEFRSDHITUAQUEDUBUCUA $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

  ${
    $d G x y z $.  $d X x y z $.
    ismnd1.1 $e |- X = dom dom G $.
    $( The predicate "is a monoid".  (Contributed by FL, 2-Nov-2009.) $)
    ismnd1 $p |- ( G e. A -> ( G e. Mnd <->
      ( G : ( X X. X ) --> X /\
        A. x e. X A. y e. X A. z e. X
          ( ( x G y ) G z ) = ( x G ( y G z ) ) /\
        E. x e. X A. y e. X
         ( ( y G x ) = y /\ ( x G y ) = y ) ) ) ) $=
      ( wcel cmnd csem cv co wceq wa wral wrex cxp wf w3a ismnd smgrpmgm
      ad2antrl smgrpass simprr 3jca issmgrp 3simpa syl5bir imp 3simp3 adantl
      jca impbida bitrd ) EDHZEIHEJHZBKZAKZELUQMURUQELZUQMNBFOAFPZNZFFQFERZUSCK
      ZELURUQVCELELMCFOBFOAFOZUTSZABDEFGTUOVAVEUOVANVBVDUTUPVBUOUTEFGUAUBUPVDUO
      UTABCEFGUCUBUOUPUTUDUEUOVENUPUTUOVEUPUOUPVBVDNVEABCDEFGUFVBVDUTUGUHUIVEUT
      UOVBVDUTUJUKULUMUN $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

  ${
    $d G x y z $.  $d X x y z $.
    ismnd2.1 $e |- X = ran G $.
    $( The predicate "is a monoid".  (Contributed by FL, 2-Nov-2009.) $)
    ismnd2 $p |- ( G e. A -> ( G e. Mnd <->
      ( G : ( X X. X ) --> X /\
        A. x e. X A. y e. X A. z e. X
          ( ( x G y ) G z ) = ( x G ( y G z ) ) /\
        E. x e. X A. y e. X
         ( ( y G x ) = y /\ ( x G y ) = y ) ) ) ) $=
      ( wcel cmnd cxp wf cv co wceq wral wa wrex w3a crn cdm cmagm cexid cin
      mndismgm mndisexid jca elin sylibr rngopid syl wi eqid ismnd1 wb eqtr
      xpid11 biimpri ancri eqcoms mpan feq23 raleq1 raleqd rexeqd 3anbi123d
      biimpcd syl6bi mpdi fdm dmeq dmxpid mpancom 3exp com4r 3imp biimprcd
      syl6 mpan2 3syl pm2.43i com12 impbid ) EDHZEIHZFFJZFEKZALZBLZEMZCLZEMWGWH
      WJEMEMNZCFOZBFOZAFOZWHWGEMWHNWIWHNPZBFOZAFQZRZWCWDESZETZTZNZWRWDEUAUBUCHZ
      XBWDEUAHZEUBHZPXCWDXDXEEUDEUEUFEUAUBUGUHEUIUJWCWDXAXAJZXAEKZWKCXAOZBXAOZA
      XAOZWOBXAOZAXAQZRZXBWRUKABCDEXAXAULUMZXBXMWRXBXGWFXJWNXLWQXBXFWENZXAFNZPZ
      XGWFUNFWSNZXBXQGXRXBPZFXANZXQFWSXAUOZXQXAFXPXOXOXPXAFUPUQURUSUJUTXFXAWEFE
      VAUJXRXBXJWNUNZGXSXTYBYAYBXAFXIWMAXAFXHWLBXAFWKCXAFVBVCVCUSUJUTXRXBXLWQUN
      ZGXSXTYCYAYCXAFXKWPAXAFWOBXAFVBVDUSUJUTVEVFVGVHWRWCWDWFWNWQWCWDUKZWFWNWQY
      DUKZUKZWFWTWENXAWETZNZWFYFUKZWEFEVIWTWEVJYHYGFNZYIFVKYHYJPXPYIXAYGFUOXPWF
      WNYEXPWFWNRWQXMYDXPWFWNWQXMUKWFWNWQXPXMWFWNWQXPXMUKXPWRXMWRXMUNFXAXTWFXGW
      NXJWQXLWEXFNZXTWFXGUNYKXTFXAUPUQWEFXFXAEVAVLWMXIAFXAWLXHBFXAWKCFXAVBVCVCW
      PXKAFXAWOBFXAVBVDVEUSVFVMVNVOWCWDXMXNVPVQVMUJVRVSVTVOWAWB $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

  ${
    $d G w x y z $.
    $( A group is a monoid.  (Contributed by FL, 2-Nov-2009.) $)
    grpmnd $p |- ( G e. Grp -> G e. Mnd ) $=
      ( vx vy vz vw cgr wcel cmnd crn cxp wf cv co wceq wral wrex wa w3a eqid
      isgrp biimpd wi grpidinv pm3.26 r19.20si r19.22si ismnd2 wb ancom a1i
      rexralbidv 3anbi3d bitrd biimprcd 3exp impcom com3l syl mpcom com12 ex
      com13 3imp syli pm2.43i ) AFGZAHGZVFVFAIZVHJVHAKZBLZCLZAMZDLZAMVJVKVMAMAM
      NDVHOCVHOBVHOZELZVJAMVJNVKVJAMZVONCVHPQBVHOEVHPZRZVGVFVFVRBCDEFAVHVHSZTUA
      VIVNVQVFVGUBZVQVNVIVTVNVIVTUBUBVQVNVIVTVFVNVIQZVGVLVKNZVPVKNZQZVOVKAMVJNV
      KVOAMVJNQEVHPZQZCVHOZBVHPZVFWAVGUBZCEBAVHVSUCWHWDCVHOZBVHPZVFWIUBWGWJBVHW
      FWDCVHWDWEUDUEUFWAWKVFVGVIVNWKVTUBVIVNWKVTVFVGVIVNWKRZVFVGVIVNWCWBQZCVHOB
      VHPZRWLBCDFAVHVSUGVFWNWKVIVNVFWMWDBCVHVHWMWDUHVFWCWBUIUJUKULUMUNUOUPUQURU
      SUTVAUJVBVCVDVE $.
      $( [3-Nov-2009] $) $( [2-Nov-2009] $)
  $}

