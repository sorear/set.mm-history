$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Connectedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Con $.
  $( Extend class notation with the the class of all connected topologies. $)
  ccon $a class Con $.

  $( Topologies are connected when only ` (/) ` and ` U. j ` are both open and
     closed. $)
  df-con $a |- Con = { j e. Top | ( j i^i ( Clsd ` j ) ) = { (/) , U. j } } $.

  ${
    $d J j $.
    $( The predicate ` J ` is a connected topology .  (Contributed by FL,
       17-Nov-2008.) $)
    iscon $p |- ( J e. Top -> ( J e. Con <-> ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj ccon wcel ctop ccld cfv cin c0 cuni cpr wceq cv fveq2 ineq12d preq2d
      id unieq eqeq12d df-con elrab2 baib ) ACDAEDAAFGZHZIAJZKZLZBMZUHFGZHZIUHJ
      ZKZLUGBAECUHALZUJUDULUFUMUHAUIUCUMQUHAFNOUMUKUEIUHARPSBTUAUB $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)

    $( The predicate ` J ` is a connected topology .  (Contributed by FL,
       17-Nov-2008.) $)
    iscon2 $p |- ( J e. Con <-> ( J e. Top /\ ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj cv ccld cfv cin c0 cuni wceq ctop ccon id fveq2 ineq12d unieq preq2d
      cpr eqeq12d df-con elrab2 ) BCZUADEZFZGUAHZQZIAADEZFZGAHZQZIBAJKUAAIZUCUG
      UEUIUJUAAUBUFUJLUAADMNUJUDUHGUAAOPRBST $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)

  $}

  ${
    $d J x y $.  $d X y $.
    usinuniop.1 $e |- X = U. J $.
    $( If a topology is connected, its underlying set can't be partitioned into
       two non empty non-overlapping open sets.  (Contributed by FL,
       17-Nov-2008.) $)
    usinuniop $p |- ( J e. Con -> A. x e. J A. y e. J
       ( ( x =/= (/) /\ y =/= (/) /\ ( x i^i y ) = (/) )
         -> X =/= ( x u. y ) ) ) $=
      ( wcel c0 wne wceq wi wa wn wo wal wb impcom sylibr sylbir ex sylbi eleq1
      ccon cv cin w3a cun cdif ctop ccld cfv cuni cpr iscon2 opncld elin biimpd
      eleq2 visset elpr olc a1i com13 eqcomi eqeq2i eqeq2 wss eltopss dfss2 cab
      df-dif eqtr abeq2 pm4.71 albii 19.26 imim1 pm2.01 syl6 eq0 syl eqcoms mpi
      alimi com12 imp orcd syl6bi jaoi syl6com com24 expcom pm2.43a com3l com14
      mpancom imp41 nne orbi12i ianor con2d sylanb 3adant3 elssuni eqcom sseq2i
      uneqdifeq syl5bb 3ad2ant3 syl5com ad2antrl necon3abid mpbird r19.21aivv
      exp31 ) CUBFZAUCZGHZBUCZGHZXPXRUDGIZUEZDXPXRUFZHZJABCCXOXPCFZXRCFZKZYAYCX
      OYFKZYAKZYCDXPUGZXRIZLZYAYGYKXQXSYGYKJXTYGXQXSKZYKXOCUHFZCCUIUJZUDZGCUKZU
      LZIZKZYFYLYKJCUMYSYFKZYJYLYTYJYLLZYTYJKZXQLZXSLZMZUUAUUBXPGIZXRGIZMZUUEYM
      YRYFYJUUHYFYRYMYJUUHJZYEYDYRYMUUIJJYMYDYRYEUUIYMYDYRYEUUIJJZYIYNFZYMYDKZU
      UJXPCDEUNUUKUULKYJYEYRUUHUUKUULYJYEYRUUHJZJZJYJUUKUULUUNUUKYJUULUUNJZYJUU
      KXRYNFZYJUUOJYIXRYNUAUUPYEUULYJUUMYEUUPUULYJUUMJJZYEUUPKXRYOFZUUQXRCYNUOU
      URYRYJUULUUHYRUURXRYQFZYJUULUUHJZJZYRUURUUSYOYQXRUQUPUUSUUGXRYPIZMUVAXRGY
      PBURUSUUGUVAUVBUULYJUUGUUHYJUUGUUHJZJUULUVCYJUUGUUFUTVAVAVBUVBXRDIZUVAYPD
      XRDYPEVCZVDUVDYJYIDIZUUTXRDYIVEUULUVFUUHUULXPDVFZUVFUUHJXPCDEVGUVGUVFUUHU
      VGUVFKUUFUUGUVGUVFUUFUVGXRXPFZXRDFZJZBNZUVFUUFJBXPDVHUVFUVKUUFUVFYIUVIUVH
      LZKZBVIZIZUVKUUFJZBDXPVJUVOUVPJDYIDYIIZUVOUVPUVQUVOKDUVNIZUVPDYIUVNVKUVRU
      VIUVMOZBNZUVPUVMBDVLUVTUVIUVLJZBNZUVPUWAUVSBUVIUVLVMVNUWBUVKUUFUWBUVKKUWA
      UVJKZBNZUUFUWAUVJBVOUWDUVLBNUUFUWCUVLBUVJUWAUVLUVJUWAUVHUVLJUVLUVHUVIUVLV
      PUVHVQVRPWCBXPVSQRSRTVTSWAWBWDTWEWFSVTWDWGTWHTWIWJRWKWJWGWLWMWEWJWOSWNPVB
      WPUUCUUFUUDUUGXPGWQXRGWQWRQXQXSWSQSWTXAWDXBPYHYJDYBYGYADYBIZYJOZYDYAUWFJX
      OYEYDXPYPVFZYAUWFXPCXCXTXQUWGUWFJXSUWGXTUWFUWEYBDIZUWGXTKYJDYBXDUWGUVGXTU
      WHYJOYPDXPUVEXEXPXRDXFXAXGWKXHXIXJWEXKXLXNXM $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)
  $}

  $( A connected topology is a topology.  (Contributed by FL, 22-Dec-2008.) $)
  contop $p |- ( J e. Con -> J e. Top ) $=
    ( ccon wcel ctop ccld cfv cin c0 cuni cpr wceq iscon2 simplbi ) ABCADCAAEFG
    HAIJKALM $.
    $( [14-Dec-2013] $) $( [22-Dec-2008] $)

  ${
    $d x y F $.
    $( A filter gives rise to a connected topology.  (Contributed by Jeff
       Hankins, 6-Dec-2009.)  (Revised by Mario Carneiro, 14-Dec-2013.) $)
    filcon $p |- ( F e. Fil -> ( F u. { (/) } ) e. Con ) $=
      ( vx vy wcel c0 cun cin cuni wceq wa wo wi wss wral cvv syl eleq1 wn elun
      ex cfil csn ccon ccld cfv cpr cv cdif ctop wal wb snex unexg mpan2 istopg
      uni0b ssun2 0ex snid sselii mpbiri sylbir a1i wex nss ssel2 adantll sylib
      simpll orcomd ord uniss ad2antlr uniun unisn uneq2i un0 3eqtrri syl6sseqr
      impr elssuni ad2antrl w3a eqid fillsb imp syl13anc elun1 19.23adv pm2.61d
      syl5bi 19.21aiv filint 3expa r19.21aiva elsni ineq2 sseqin2 mpbi syl6eqel
      0ss syl6eq rgen ralun sylancl ineq1 a1d r19.21aiv mpbir2and cldopn adantr
      df-ss orim2i sylbi cfbas filfbas fbncp pm2.21d jaod adantl ssdif0 biimpri
      sylan eqss syl2im orim12d syl5 syld expimpd 0cld jctil anbi12d syl5ibrcom
      filusb topcld jca impbid elin visset elpr eqeq2i orbi2i bitr4i 3bitr4g
      eqrdv iscon mpbird ) AUADZAEUBZFZUCDZUUJUUJUDUEZGZEUUJHZUFZIZUUHBUUMUUOUU
      HBUGZUUJDZUUQUULDZJZUUQEIZUUQAHZIZKZUUQUUMDUUQUUODZUUHUUTUVDUUHUURUUSUVDU
      UHUURJZUUSUVBUUQUHZUUJDZUVDUUHUUSUVHLZUURUUHUUJUIDZUVIUUHUVJUUQUUJMZUUQHZ
      UUJDZLZBUJZUUQCUGZGZUUJDZCUUJNZBUUJNZUUHUUJODZUVJUVOUVTJUKUUHUUIODUWAEULA
      UUIUAOUMUNBCOUUJUOPUUHUVNBUUHUVKUVMUUHUVKJZUUQUUIMZUVMUWCUVMLUWBUWCUVLEIZ
      UVMUUQUPUWDUVMEUUJDZUUIUUJEUUIAUQEURUSUTZUVLEUUJQVAVBVCUWCRUVPUUQDZUVPUUI
      DZRZJZCVDUWBUVMCUUQUUIVEUWBUWJUVMCUWBUWJUVMUWBUWJJZUVLADZUVMUWKUUHUVPADZU
      VLUVBMZUVPUVLMZUWLUUHUVKUWJVIUWBUWGUWIUWMUWBUWGJZUWHUWMUWPUWMUWHUWPUVPUUJ
      DZUWMUWHKUVKUWGUWQUUHUUQUUJUVPVFVGUVPAUUISVHVJVKVTUWKUVLUUNUVBUVKUVLUUNMU
      UHUWJUUQUUJVLVMUUNUVBUUIHZFUVBEFUVBAUUIVNUWREUVBEURVOVPUVBVQVRZVSUWGUWOUW
      BUWIUVPUUQWAWBUUHUWMUWNUWOWCUWLUVPUVLAUVBUVBWDZWEWFWGUVLAUUIWHPTWIWKWJTWL
      UUHUVSBANUVSBUUINUVTUUHUVSBAUUHUUQADZJZUVRCANUVRCUUINUVSUXBUVRCAUUHUXAUWM
      UVRUUHUXAUWMWCUVQADUVRUUQUVPAWMUVQAUUIWHPWNWOUVRCUUIUWHUVPEIZUVRUVPEWPUXC
      UVQEUUJUXCUVQUUQEGZEUVPEUUQWQEUUQMUXDEIUUQXAEUUQWRWSXBUWFWTPXCUVRCAUUIXDX
      EWOUVSBUUIUUQUUIDZUVAUVSUUQEWPZUVAUVRCUUJUVAUVRUWQUVAUVQEUUJUVAUVQEUVPGZE
      UUQEUVPXFEUVPMUXGEIUVPXAEUVPXLWSXBUWFWTXGXHPXCUVSBAUUIXDXEXIZUVJUUSUVHUUQ
      UUJUVBUWSXJTPXKUVHUVGADZUVGEIZKZUVFUVDUVHUXIUVGUUIDZKUXKUVGAUUISUXLUXJUXI
      UVGEWPXMXNUVFUXIUVAUXJUVCUUHUURUXIUVALZUURUXAUXEKUUHUXMUUQAUUISUUHUXAUXMU
      XEUUHUXAUXMUXBUXIUVAUUHAXODUXAUXIRAXPUUQUVBAXQYCXRTUXEUXMLUUHUXEUVAUXIUXF
      XGVCXSWKWFUVFUUQUVBMZUXJUVBUUQMZUVCUURUXNUUHUURUUQUUNUVBUUQUUJWAUWSVSXTUX
      OUXJUVBUUQYAYBUXNUXOUVCUVCUXNUXOJUUQUVBYDYBTYEYFYGYHYIUUHUVAUUTUVCUUHUUTU
      VAUWEEUULDZJUUHUXPUWEUUHUVJUXPUXHUUJYJPUWFYKUVAUURUWEUUSUXPUUQEUUJQUUQEUU
      LQYLYMUUHUUTUVCUVBUUJDZUVBUULDZJUUHUXQUXRUUHUVBADUXQAUVBUWTYNUVBAUUIWHPUU
      HUVJUXRUXHUUJUVBUWSYOPYPUVCUURUXQUUSUXRUUQUVBUUJQUUQUVBUULQYLYMXSYQUUQUUJ
      UULYRUVEUVAUUQUUNIZKUVDUUQEUUNBYSYTUVCUXSUVAUVBUUNUUQUWSUUAUUBUUCUUDUUEUU
      HUVJUUKUUPUKUXHUUJUUFPUUG $.
      $( [14-Dec-2013] $) $( [6-Dec-2009] $)
  $}


