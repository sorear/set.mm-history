$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Connectedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Con $.
  $( Extend class notation with the the class of all connected topologies. $)
  ccon $a class Con $.

  $( Topologies are connected when only ` (/) ` and ` U. j ` are both open and
     closed. $)
  df-con $a |- Con = { j e. Top | ( j i^i ( Clsd ` j ) ) = { (/) , U. j } } $.

  ${
    $d J j $.
    $( The predicate ` J ` is a connected topology .  (Contributed by FL,
       17-Nov-2008.) $)
    iscon $p |- ( J e. Top -> ( J e. Con <-> ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj ccon wcel ctop ccld cfv cin c0 cuni cpr wceq cv fveq2 ineq12d preq2d
      id unieq eqeq12d df-con elrab2 baib ) ACDAEDAAFGZHZIAJZKZLZBMZUHFGZHZIUHJ
      ZKZLUGBAECUHALZUJUDULUFUMUHAUIUCUMQUHAFNOUMUKUEIUHARPSBTUAUB $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)

    $( The predicate ` J ` is a connected topology .  (Contributed by FL,
       17-Nov-2008.) $)
    iscon2 $p |- ( J e. Con <-> ( J e. Top /\ ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj cv ccld cfv cin c0 cuni wceq ctop ccon id fveq2 ineq12d unieq preq2d
      cpr eqeq12d df-con elrab2 ) BCZUADEZFZGUAHZQZIAADEZFZGAHZQZIBAJKUAAIZUCUG
      UEUIUJUAAUBUFUJLUAADMNUJUDUHGUAAOPRBST $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)
  $}

  ${
    $d J x y $.  $d X y $.
    usinuniop.1 $e |- X = U. J $.
    $( If a topology is connected, its underlying set can't be partitioned into
       two non empty non-overlapping open sets.  (Contributed by FL,
       17-Nov-2008.) $)
    usinuniop $p |- ( J e. Con -> A. x e. J A. y e. J
       ( ( x =/= (/) /\ y =/= (/) /\ ( x i^i y ) = (/) )
         -> X =/= ( x u. y ) ) ) $=
      ( wcel c0 wne wceq wi wa wn wo wal wb impcom sylibr sylbir ex sylbi eleq1
      ccon cv cin w3a cun cdif ctop ccld cfv cuni cpr iscon2 opncld elin biimpd
      eleq2 vex elpr olc a1i com13 eqcomi eqeq2i eqeq2 wss eltopss dfss2 df-dif
      cab eqtr abeq2 pm4.71 albii 19.26 imim1 pm2.01 alimi eq0 syl eqcoms com12
      syl6 mpi imp orcd syl6bi syl6com com24 expcom pm2.43a com3l mpancom com14
      jaoi imp41 nne orbi12i ianor con2d sylanb 3adant3 elssuni eqcom uneqdifeq
      sseq2i syl5bb 3ad2ant3 syl5com ad2antrl necon3abid mpbird exp31 ralrimivv
      ) CUBFZAUCZGHZBUCZGHZXPXRUDGIZUEZDXPXRUFZHZJABCCXOXPCFZXRCFZKZYAYCXOYFKZY
      AKZYCDXPUGZXRIZLZYAYGYKXQXSYGYKJXTYGXQXSKZYKXOCUHFZCCUIUJZUDZGCUKZULZIZKZ
      YFYLYKJCUMYSYFKZYJYLYTYJYLLZYTYJKZXQLZXSLZMZUUAUUBXPGIZXRGIZMZUUEYMYRYFYJ
      UUHYFYRYMYJUUHJZYEYDYRYMUUIJJYMYDYRYEUUIYMYDYRYEUUIJJZYIYNFZYMYDKZUUJXPCD
      EUNUUKUULKYJYEYRUUHUUKUULYJYEYRUUHJZJZJYJUUKUULUUNUUKYJUULUUNJZYJUUKXRYNF
      ZYJUUOJYIXRYNUAUUPYEUULYJUUMYEUUPUULYJUUMJJZYEUUPKXRYOFZUUQXRCYNUOUURYRYJ
      UULUUHYRUURXRYQFZYJUULUUHJZJZYRUURUUSYOYQXRUQUPUUSUUGXRYPIZMUVAXRGYPBURUS
      UUGUVAUVBUULYJUUGUUHYJUUGUUHJZJUULUVCYJUUGUUFUTVAVAVBUVBXRDIZUVAYPDXRDYPE
      VCZVDUVDYJYIDIZUUTXRDYIVEUULUVFUUHUULXPDVFZUVFUUHJXPCDEVGUVGUVFUUHUVGUVFK
      UUFUUGUVGUVFUUFUVGXRXPFZXRDFZJZBNZUVFUUFJBXPDVHUVFUVKUUFUVFYIUVIUVHLZKZBV
      JZIZUVKUUFJZBDXPVIUVOUVPJDYIDYIIZUVOUVPUVQUVOKDUVNIZUVPDYIUVNVKUVRUVIUVMO
      ZBNZUVPUVMBDVLUVTUVIUVLJZBNZUVPUWAUVSBUVIUVLVMVNUWBUVKUUFUWBUVKKUWAUVJKZB
      NZUUFUWAUVJBVOUWDUVLBNUUFUWCUVLBUVJUWAUVLUVJUWAUVHUVLJUVLUVHUVIUVLVPUVHVQ
      WCPVRBXPVSQRSRTVTSWAWDWBTWEWFSVTWBWGTWOTWHWIRWJWIWGWKWLWEWIWMSWNPVBWPUUCU
      UFUUDUUGXPGWQXRGWQWRQXQXSWSQSWTXAWBXBPYHYJDYBYGYADYBIZYJOZYDYAUWFJXOYEYDX
      PYPVFZYAUWFXPCXCXTXQUWGUWFJXSUWGXTUWFUWEYBDIZUWGXTKYJDYBXDUWGUVGXTUWHYJOY
      PDXPUVEXFXPXRDXEXAXGWJXHXIXJWEXKXLXMXN $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)
  $}

  ${
    $d o x y J $.  $d o x y X $.
    dfcon2.1 $e |- X = U. J $.
    $( An alternate definition of connectedness.  (Contributed by Jeff Hankins,
       9-Jul-2009.) $)
    dfcon2 $p |- ( J e. Top -> ( J e. Con <-> A. x e. J A. y e. J ( ( x =/= (/)
    /\ y =/= (/) /\ ( x i^i y ) = (/) ) -> X =/= ( x u. y ) ) ) ) $=
      ( vo wcel c0 wne cin wceq wi wa wn adantl ex adantr wb eleq1 mpbird cv wo
      ctop ccon w3a cun wral usinuniop ccld cfv cuni cdif simprl cldopn adantrl
      cpr neeq1 ineq1 eqeq1d 3anbi13d uneq1 neeq2d imbi12d ineq2 3anbi23d uneq2
      rcla42v syl2anc disjdif jctr df-3an sylibr imim1i id necon2bd wss eltopss
      undif biimpi eqcomd pm2.27 df-ne anbi12i notbii oran bitr4i eqss simplbi2
      ssdif0 eqeq2i 3imtr3g orim2d com12 sylbi syl6 com3r mpd adantrr syl5 syld
      syldan com23 imp 0opn 0cld jca eqid topopn topcld jaod impbid vex 3bitr4g
      elin elpr eqrdv iscon sylibrd impbid2 ) CUCGZCUDGZAUAZHIZBUAZHIZYBYDJZHKZ
      UEZDYBYDUFZIZLZBCUGACUGZABCDEUHXTYLCCUIUJZJZHCUKZUPZKZYAXTYLYQXTYLMZFYNYP
      YRFUAZCGZYSYMGZMZYSHKZYSYOKZUBZYSYNGYSYPGYRUUBUUEXTYLUUBUUELXTUUBYLUUEXTU
      UBYLUUELXTUUBMZYLYSHIZDYSULZHIZYSUUHJZHKZUEZDYSUUHUFZIZLZUUEUUFYTUUHCGZYL
      UUOLXTYTUUAUMXTUUAUUPYTYSCDEUNUOYKUUOUUGYEYSYDJZHKZUEZDYSYDUFZIZLABYSUUHC
      CYBYSKZYHUUSYJUVAUVBYCUUGYGUURYEYBYSHUQUVBYFUUQHYBYSYDURUSUTUVBYIUUTDYBYS
      YDVAVBVCYDUUHKZUUSUULUVAUUNUVCYEUUIUURUUKUUGYDUUHHUQUVCUUQUUJHYDUUHYSVDUS
      VEUVCUUTUUMDYDUUHYSVFVBVCVGVHUUOUUGUUIMZUUNLZUUFUUEUVDUULUUNUVDUVDUUKMUUL
      UVDUUKYSDVIVJUUGUUIUUKVKVLVMUVEDUUMKZUVDNZLZUUFUUEUVEUVDDUUMUVEVNVOXTYTUV
      HUUELZUUAXTYTYSDVPZUVIYSCDEVQXTUVJMZUVFUVIUVJUVFXTUVJUUMDUVJUUMDKYSDVRVSV
      TOUVFUVHUVKUUEUVFUVHUVGUVKUUELZUVFUVGWAUVGUUCUUHHKZUBZUVLUVGUUCNZUVMNZMZN
      UVNUVDUVQUUGUVOUUIUVPYSHWBUUHHWBWCWDUUCUVMWEWFUVKUVNUUEUVKUVMUUDUUCUVKDYS
      VPZYSDKZUVMUUDUVJUVRUVSLXTUVSUVJUVRYSDWGWHODYSWIDYOYSEWJWKWLWMWNWOWPWQXAW
      RWSWSWTPXBXCXTUUEUUBLYLXTUUCUUBUUDXTUUCUUBXTUUCMZYTUUAUVTYTHCGZXTUWAUUCCX
      DQUUCYTUWARXTYSHCSOTUVTUUAHYMGZXTUWBUUCCXEQUUCUUAUWBRXTYSHYMSOTXFPXTUUDUU
      BXTUUDMZYTUUAUWCYTYOCGZXTUWDUUDCYOYOXGZXHQUUDYTUWDRXTYSYOCSOTUWCUUAYOYMGZ
      XTUWFUUDCYOUWEXIQUUDUUAUWFRXTYSYOYMSOTXFPXJQXKYSCYMXNYSHYOFXLXOXMXPPCXQXR
      XS $.
      $( [9-Jul-2009] $)
  $}

  ${
    $d r s u v x y J $.  $d r s u v x y S $.  $d r s u v x y X $.
    connsub.1 $e |- X = U. J $.
    $( Two equivalent ways of saying that a subspace topology is connected.
       (Contributed by Jeff Hankins, 9-Jul-2009.) $)
    connsub $p |- ( ( J e. Top /\ S C_ X ) -> ( ( subSp ` <. S , J >. ) e. Con
    <-> A. x e. J A. y e. J ( ( ( x i^i S ) =/= (/) /\ ( y i^i S ) =/= (/) /\ (
    x i^i y ) C_ ( X \ S ) ) -> -. S C_ ( x u. y ) ) ) ) $=
      ( vu vv vr vs wcel wss wa c0 wne cin wceq w3a wi cvv ctop cop csubsp ccon
      cfv cv cuni cun wral cdif wn wb topopn ssexg syl2anr stoig3 syldan dfcon2
      id eqid syl sseq2i stoig2 neeq1d imbi2d 2ralbidv simp1l 3ad2ant1 elsubspr
      sylan2b simp2l syl3anc simp2r neeq1 ineq1 eqeq1d 3anbi13d neeq2d 3anbi23d
      uneq1 imbi12d ineq2 uneq2 rcla42v syl2anc inindir difss sstr reldisj ibir
      mpan2 syl5eqr 3anim3i 3ad2ant3 incom df-ss biimpi syl5reqr indir necon3ai
      syl6eq embantd syld 3exp com34 com23 ralrimdvv wrex uniexg ancoms elsubsp
      a1i syl2an anbi12d reeanv sseq1d sseq2d notbid 3ad2ant2 3ad2antr1 adantlr
      biimpa 3ad2antl3 3ad2antr2 adantll ineq12 syl5eq eltopss ssinss1 ad2ant2r
      3ad2antr3 3adant3 adantr mpbid 3jca inss1 sseq1 mpbiri anim12i unss12 a1d
      syl5ibrcom syl3an3 imp necon3bd rexlimdvv syl5bir sylbid impbid 3bitr2d
      ex ) DUAKZCELZMZCDUBUCUEZUDKZGUFZNOZHUFZNOZUUQUUSPZNQZRZUUOUGZUUQUUSUHZOZ
      SZHUUOUIGUUOUIZUVCCUVEOZSZHUUOUIGUUOUIZAUFZCPZNOZBUFZCPZNOZUVLUVOPZECUJZL
      ZRZCUVLUVOUHZLZUKZSZBDUIADUIZUUNUUOUAKZUUPUVHULUULUUMCTKZUWGUUMUUMEDKUWHU
      ULUUMUSDEFUMCEDUNUOZCDTUPUQGHUUOUVDUVDUTURVAUUNUVJUVGGHUUOUUOUUNUVIUVFUVC
      UUNCUVDUVEUUMUULCDUGZLZCUVDQEUWJCFVBZCDVCVJVDVEVFUUNUVKUWFUUNUVKUWEABDDUU
      NUVLDKZUVODKZMZUVKUWEUUNUWOUWAUVKUWDUUNUWOUWAUVKUWDSUUNUWOUWARZUVKUVNUVQU
      VMUVPPZNQZRZCUVMUVPUHZOZSZUWDUWPUVMUUOKZUVPUUOKZUVKUXBSUWPUULUWHUWMUXCUUL
      UUMUWOUWAVGZUUNUWOUWHUWAUWIVHZUUNUWMUWNUWAVKUVLCDTVIVLUWPUULUWHUWNUXDUXEU
      XFUUNUWMUWNUWAVMUVOCDTVIVLUVJUXBUVNUUTUVMUUSPZNQZRZCUVMUUSUHZOZSGHUVMUVPU
      UOUUOUUQUVMQZUVCUXIUVIUXKUXLUURUVNUVBUXHUUTUUQUVMNVNUXLUVAUXGNUUQUVMUUSVO
      VPVQUXLUVEUXJCUUQUVMUUSVTVRWAUUSUVPQZUXIUWSUXKUXAUXMUUTUVQUXHUWRUVNUUSUVP
      NVNUXMUXGUWQNUUSUVPUVMWBVPVSUXMUXJUWTCUUSUVPUVMWCVRWAWDWEUWPUWSUXAUWDUWAU
      UNUWSUWOUVTUWRUVNUVQUVTUWQUVRCPZNUVLUVOCWFUVTUXNNQZUVTUVRELZUXOUVTULUVTUV
      SELUXPECWGUVRUVSEWHWKUVRCEWIVAWJWLWMWNUXAUWDSUWPUWCCUWTUWCCUWBCPZUWTUWCUX
      QCUWBPZCCUWBWOUWCUXRCQCUWBWPWQWRUVLUVOCWSXAWTXLXBXCXDXEXFXGUUNUWFUVJGHUUO
      UUOUUNUUQUUOKZUUSUUOKZMZUWFUVJUUNUYAUUQIUFZCPZQZIDXHZUUSJUFZCPZQZJDXHZMZU
      WFUVJSZUULUUMUWHUYAUYJULUULUWJTKZUWKUWHUUMDUAXIUUMUWKUWLWQUWKUYLUWHCUWJTU
      NXJXMUULUWHMUXSUYEUXTUYIIUUQCDTXKJUUSCDTXKXNUQUYJUYDUYHMZJDXHIDXHUUNUYKUY
      DUYHIJDDXOUUNUYMUYKIJDDUUNUYBDKZUYFDKZMZUYMUYKUUNUYPUYMRZUWFUYCNOZUYGNOZU
      YBUYFPZUVSLZRZCUYBUYFUHZLZUKZSZUVJUYPUUNUWFVUFSUYMUWEVUFUYRUVQUYBUVOPZUVS
      LZRZCUYBUVOUHZLZUKZSABUYBUYFDDUVLUYBQZUWAVUIUWDVULVUMUVNUYRUVTVUHUVQVUMUV
      MUYCNUVLUYBCVOVDVUMUVRVUGUVSUVLUYBUVOVOXPVQVUMUWCVUKVUMUWBVUJCUVLUYBUVOVT
      XQXRWAUVOUYFQZVUIVUBVULVUEVUNUVQUYSVUHVUAUYRVUNUVPUYGNUVOUYFCVOVDVUNVUGUY
      TUVSUVOUYFUYBWBXPVSVUNVUKVUDVUNVUJVUCCUVOUYFUYBWCXQXRWAWDXSUYQUVCVUFUVIUY
      QUVCVUFUVISUYQUVCMZVUBVUEUVIVUOUYRUYSVUAUYMUUNUVCUYRUYPUYDUVCUYRUYHUYDUUT
      UURUYRUVBUYDUURUYRUUQUYCNVNYBXTYAYCUYMUUNUVCUYSUYPUYHUVCUYSUYDUYHUURUUTUY
      SUVBUYHUUTUYSUUSUYGNVNYBYDYEYCVUOUYTCPZNQZVUAVUOVUPUYCUYGPZNUYBUYFCWFUYQU
      URUVBVURNQZUUTUYMUUNUVBVUSUYPUYMUVBVUSUYMUVAVURNUUQUYCUUSUYGYFVPYBYCYKYGV
      UOUYTELZVUQVUAULUYQVUTUVCUUNUYPVUTUYMUULUYNVUTUUMUYOUULUYNMUYBELVUTUYBDEF
      YHUYBUYFEYIVAYJYLYMUYTCEWIVAYNYOVUOVUDCUVEUYQUVCCUVEQZVUDSZUYMUUNUYPUUQUY
      BLZUUSUYFLZMZUVCVVBSZUYDVVCUYHVVDUYDVVCUYCUYBLUYBCYPUUQUYCUYBYQYRUYHVVDUY
      GUYFLUYFCYPUUSUYGUYFYQYRYSVVEUUNVVFUYPVVEVVBUVCVVEVUDVVAUVEVUCLUUQUYBUUSU
      YFYTCUVEVUCYQUUBUUAWNUUCUUDUUEXBUUKXFXCXDUUFUUGUUHXFXGUUIUUJ $.
      $( [15-Dec-2013] $) $( [9-Jul-2009] $)
  $}

  ${
    $d s t x y z A $.
    $( Lemma for ~ reconn .  Connectedness in the reals-easy direction.
       (Contributed by Jeff Hankins, 13-Jul-2009.) $)
    reconnlem1 $p |- ( ( A C_ RR /\ ( subSp ` <. A , ( topGen ` ran (,) ) >. )
        e.
   Con ) -> A. x e. A A. y e. A ( x [,] y ) C_ A ) $=
      ( vs vt cr wss wcel cin c0 wn wi wb wa wbr syl adantr cmnf cpnf wceq cioo
      vz crn ctg cfv cop csubsp ccon cv cicc co wral wne cdif w3a ctop uniretop
      cun retop connsub mpan cxr cle rexr adantrr adantrl elicc1 syl2anc pm4.61
      ssel2 wrex mnflt nelneq eqcom necon3abii adantlr ad2ant2l simprl1 simprl2
      clt sylibr xrleltne syl3anc mpbird 3jca mnfxr elioo2 sylancr simplrl elin
      sylanbrc ne0i df-ne adantll simprl3 ltpnf pnfxr sylancl simplrr jctl jctr
      xrleid ioodisj syl21anc 3ad2ant1 ad2antrl 0ss a1i eqsstrd csn ccid ineq2d
      disjsn biimpri ad2antll eqtrd reldisj ad2antrr mpbid cuni xrlelttr 3com12
      mp3an3 exp4b com23 imp32 adantl oprex iooretop ineq1 neeq1d sseq1d sseq2d
      cpr jca anbi12d ex rexnal imp sylbid com24 3adantr2 notnot nltpnft mtbird
      mpd sylib xrltletr mp3an1 ngtmnft con2bid sylibd sylc 3adantr3 w3o biimpi
      elxr ecase23d sylan difreicc unipr syl6eq sseqtrd 3anbi13d uneq1 3anbi23d
      ineq2 uneq2 rcla42ev mp3an12 syl31anc 2rexbii rexbii 3bitri syl6ib syl5bi
      df-an con4d ssrdv ralrimdvva ) CFGZCUAUCUDUEZUFUGUEUHHZAUIZBUIZUJUKZCGZBC
      ULACULZUWAUWCDUIZCIZJUMZEUIZCIZJUMZUWIUWLIZFCUNZGZUOZCUWIUWLURZGZKLZEUWBU
      LZDUWBULZUWHUWBUPHUWAUWCUXCMUSDECUWBFUQUTVAUWAUXCUWGABCCUWAUWDCHZUWECHZNZ
      NZUXCUWGUXGUXCNZUBUWFCUXHUBUIZUWFHZUXIVBHZUWDUXIVCOZUXIUWEVCOZUOZUXICHZUX
      HUWDVBHZUWEVBHZUXJUXNMUXGUXPUXCUWAUXDUXPUXEUWAUXDNZUWDFHZUXPCFUWDVJZUWDVD
      ZPVEZQUXGUXQUXCUWAUXEUXQUXDUWAUXENZUWEFHZUXQCFUWEVJZUWEVDZPVFZQUWDUWEUXIV
      GVHUXGUXCUXNUXOLZUXGUYHUXCUYHKUXNUXOKZNZUXGUXCKZUXNUXOVIUXGUYJUWRUWTNZEUW
      BVKDUWBVKZUYKUXGUYJUYMUXGUYJNZRUXIUAUKZCIZJUMZUXISUAUKZCIZJUMZUYOUYRIZUWP
      GZCUYOUYRURZGZUYMUYNUWDUYPHZUYQUYNUWDUYOHZUXDVUEUYNVUFUXSRUWDVTOZUWDUXIVT
      OZUOZUYNUXSVUGVUHUXGUXSUYJUWAUXDUXSUXEUXTVEZQUXGVUGUYJUWAUXDVUGUXEUXRUXSV
      UGUXTUWDVLZPVEQUYNVUHUXIUWDUMZUXFUYIVULUWAUXNUXDUYIVULUXEUXDUYINUWDUXITZK
      VULUWDUXICVMVUMUXIUWDUXIUWDVNVOWAVPVQUYNUXPUXKUXLVUHVULMUXGUXPUYJUYBQUXKU
      XLUXMUYIUXGVRZUXKUXLUXMUYIUXGVSUWDUXIWBWCWDWEUYNRVBHZUXKVUFVUIMWFVUNRUXIU
      WDWGWHWDUWAUXDUXEUYJWIUWDUYOCWJWKUYPUWDWLPUYNUWEUYSHZUYTUYNUWEUYRHZUXEVUP
      UYNVUQUYDUXIUWEVTOZUWESVTOZUOZUYNUYDVURVUSUXGUYDUYJUWAUXEUYDUXDUYEVFZQUYN
      VURUWEUXIUMZUXFUYIVVBUWAUXNUXEUYIVVBUXDUXEUYINUWEUXITKVVBUWEUXICVMUWEUXIW
      MWAWNVQUYNUXKUXQUXMVURVVBMVUNUXGUXQUYJUYGQUXKUXLUXMUYIUXGWOUXIUWEWBWCWDUX
      GVUSUYJUWAUXEVUSUXDUYCUYDVUSUYEUWEWPZPVFQWEUYNUXKSVBHZVUQVUTMVUNWQUXISUWE
      WGWRWDUWAUXDUXEUYJWSUWEUYRCWJWKUYSUWEWLPUYNVUAJUWPUXNVUAJTZUXGUYIUXKUXLVV
      EUXMUXKVUOUXKNUXKVVDNUXIUXIVCOVVEUXKVUOWFWTUXKVVDWQXAUXIXBRUXIUXISXCXDXEX
      FJUWPGUYNUWPXGXHXIUYNCFUXIUXIUJUKZUNZVUCUYNCVVFIZJTZCVVGGZUYNVVHCUXIXJZIZ
      JUXNVVHVVLTZUXGUYIUXKUXLVVMUXMUXKVVFVVKCUXIXKXLXEXFUYIVVLJTZUXGUXNVVNUYIC
      UXIXMXNXOXPUWAVVIVVJMUXFUYJCVVFFXQXRXSUYNVVGUYOUYRYNXTZVUCUYNUXIFHZVVPVVG
      VVOTUXGUXSUYDNZUYJVVPUXGUXSUYDVUJVVAYOVVQUXNVVPUYIVVQUXNNZVVPUXISTZUXIRTZ
      VVRVVSUXISVTOZKZVVRVWAVWBKUYDUXNVWAUXSUYDUXKUXMVWAUXLUYDUXKUXMVWAUYDVUSUX
      KUXMVWALLVVCUYDUXMUXKVUSVWAUYDUXKUXMVUSVWALZUYDUXQUXKUXMVWCLLUYFUXQUXKUXM
      VUSVWAUXQUXKVVDUXMVUSNVWALZWQUXKUXQVVDVWDUXIUWESYAYBYCYDPYEUUAUUFYFUUBWNV
      WAUUCUUGUXNVVSVWBMZVVQUXKUXLVWEUXMUXIUUDXEYGUUEUXSUXNVVTKZUYDUXSUXKUXLVWF
      UXMUXSUXKUXLVWFUXSUXPVUGUXKUXLVWFLZLUYAVUKUXPUXKVUGVWGUXPUXKVUGUXLVWFUXPU
      XKNVUGUXLNZRUXIVTOZVWFVUOUXPUXKVWHVWILWFRUWDUXIUUHUUIUXKVWIVWFMUXPUXKVVTV
      WIUXIUUJUUKYGUULYDYEUUMYFUUNVPUXNVVPVVSVVTUUOZVVQUXKUXLVWJUXMUXKVWJUXIUUQ
      UUPXEYGUURVEUUSZVWKUXIUXIUUTVHUYOUYRRUXIUAYHUXISUAYHUVAUVBUVCUYOUWBHUYRUW
      BHUYQUYTVUBUOZVUDNZUYMRUXIYIUXISYIUYLVWMUYQUWNUYOUWLIZUWPGZUOZCUYOUWLURZG
      ZNDEUYOUYRUWBUWBUWIUYOTZUWRVWPUWTVWRVWSUWKUYQUWQVWOUWNVWSUWJUYPJUWIUYOCYJ
      YKVWSUWOVWNUWPUWIUYOUWLYJYLUVDVWSUWSVWQCUWIUYOUWLUVEYMYPUWLUYRTZVWPVWLVWR
      VUDVWTUWNUYTVWOVUBUYQVWTUWMUYSJUWLUYRCYJYKVWTVWNVUAUWPUWLUYRUYOUVGYLUVFVW
      TVWQVUCCUWLUYRUYOUVHYMYPUVIUVJUVKYQUYMUXAKZEUWBVKZDUWBVKUXBKZDUWBVKUYKUYL
      VXADEUWBUWBUWRUWTUVQUVLVXBVXCDUWBUXAEUWBYRUVMUXBDUWBYRUVNUVOUVPUVRYSYTUVS
      YQUVTYTYS $.
      $( [13-Jul-2009] $)
  $}

  ${
    reconnlem2.1 $e |- ( ph <-> ( ( ( ( b e. ( u i^i A ) /\ c e. ( v i^i A ) /\
     ( u i^i v ) C_ ( RR \ A ) ) /\ ( A C_ RR /\ ( u e. ( topGen ` ran (,) ) /\
      v e. ( topGen ` ran (,) ) ) ) ) /\ b <_ c ) /\ ( b [,] c ) C_ A ) ) $.
    $( Lemma for ~ reconn .  (Contributed by Jeff Hankins, 15-Aug-2009.) $)
    reconnlem2 $p |- ( ph -> ( b e. RR /\ c e. RR ) ) $=
      ( cv cr wcel cin wss wa sylbi inss2 sstr mpan syl ad3antrrr sseldd w3a co
      cdif cioo crn ctg cfv cle wbr cicc simprl ad2antrr simp1 simp2 jca ) AEHZ
      IJFHZIJACHZDKZIUPADILZUSILZAUPUSJZUQBHZDKZJZURVCKIDUCLZUAZUTURUDUEUFUGZJV
      CVHJMZMZMZUPUQUHUIZMUPUQUJUBDLZMZUTGVKUTVLVMVGUTVIUKULNZUSDLUTVAURDOUSDIP
      QRAVNVBGVGVBVJVLVMVBVEVFUMSNTAVDIUQAUTVDILZVOVDDLUTVPVCDOVDDIPQRAVNVEGVGV
      EVJVLVMVBVEVFUNSNTUO $.
      $( [15-Aug-2009] $)
  $}

  ${
    $d t w z ph $.  $d t w z A $.  $d b t w z $.  $d c t w z $.  $d t u w z $.
    $d t v w z $.
    reconnlem3.1 $e |- ( ph <-> ( ( ( ( b e. ( u i^i A ) /\ c e. ( v i^i A ) /\
     ( u i^i v ) C_ ( RR \ A ) ) /\ ( A C_ RR /\ ( u e. ( topGen ` ran (,) ) /\
      v e. ( topGen ` ran (,) ) ) ) ) /\ b <_ c ) /\ ( b [,] c ) C_ A ) ) $.
    reconnlem3.2 $e |- S = sup ( ( u i^i ( b [,] c ) ) , RR , < ) $.
    $( Lemma for ~ reconn .  (Contributed by Jeff Hankins, 16-Aug-2009.) $)
    reconnlem3 $p |- ( ph -> S e. ( b [,] c ) ) $=
      ( vw vt vz cv wcel cr cle wbr w3a wss wa cicc co cin clt csup c0 wne wral
      wrex reconnlem2 inss2 a1i cdif crn ctg cfv inss1 sseli 3ad2ant1 ad3antrrr
      cioo biid simplr lbicc2 3expa syl2anc elin 3imtr4i iccsupr syl3anc suprcl
      jca syl syl5eqel sylanbrc suprub breq2i 3simpa simprd cxr wi rexr anim12i
      iccleub 3expia syl5 3syl ralrimiv suprleub syl12anc breq1i 3jca wb elicc2
      mpbird ) AEFMZGMZUAUBZNZEONZWPEPQZEWQPQZRZAWTXAXBAECMZWRUCZOUDUEZOIAXEOSZ
      XEUFUGZJMKMPQJXEUHKOUIZRZXFONAWPONZWQONZTZXEWRSZWPXENZXJABCDFGHUJZXNAXDWR
      UKZULWPXDDUCZNZWQBMZDUCNZXDXTUCODUMSZRZDOSXDVAUNUOUPZNXTYDNTTZTZWPWQPQZTW
      RDSZTZWPXDNZWPWRNZTAXOYIYJYKYCYJYEYGYHXSYAYJYBXRXDWPXDDUQURUSUTZYIXMYGYKY
      IBCDFGYIVBUJZYFYGYHVCXKXLYGYKWPWQVDVEVFZVLHWPXDWRVGZVHKJWPWQWPXEVIZVJKJXE
      VKVMVNYIWPXFPQZAXAYIXJXOYQYIXMXNXOXJYMXNYIXQULZYIYJYKXOYLYNYOVOZYPVJYSKJX
      EWPVPVFHEXFWPPIVQVHYIXFWQPQZAXBYIXGXHTZXLLMZWQPQZLXEUHYTYIXMXNXOUUAYMYRYS
      XMXNXORXJUUAYPXGXHXIVRVMVJYIXKXLYMVSYIUUCLXEYIXMWPVTNZWQVTNZTZUUBXENZUUCW
      AYMXKUUDXLUUEWPWBWQWBWCUUGUUBWRNZUUFUUCXEWRUUBXQURUUDUUEUUHUUCWPWQUUBWDWE
      WFWGWHLXEWQWIWJHEXFWQPIWKVHWLAXMWSXCWMXPWPWQEWNVMWO $.
      $( [16-Aug-2009] $)
  $}

  ${
    $d r ph $.  $d r t S $.  $d b r s t $.  $d c r s t $.  $d r s t u $.
    reconnlem4.1 $e |- ( ph <-> ( ( ( ( b e. ( u i^i A ) /\ c e. ( v i^i A ) /\
     ( u i^i v ) C_ ( RR \ A ) ) /\ ( A C_ RR /\ ( u e. ( topGen ` ran (,) ) /\
      v e. ( topGen ` ran (,) ) ) ) ) /\ b <_ c ) /\ ( b [,] c ) C_ A ) ) $.
    reconnlem4.2 $e |- S = sup ( ( u i^i ( b [,] c ) ) , RR , < ) $.
    $( Lemma for ~ reconn .  (Contributed by Jeff Hankins, 16-Aug-2009.) $)
    reconnlem4 $p |- ( ph -> -. S e. u ) $=
      ( wcel cle wbr co cr wa syl2anc clt adantr cc0 wb vt vs cicc cin wral wss
      vr cv wne wrex w3a reconnlem2 inss2 a1i cdif cioo crn ctg cfv inss1 sseli
      c0 3ad2ant1 ad3antrrr biid simplr lbicc2 jca elin 3imtr4i iccsupr syl3anc
      3expa csup suprub syl6breqr ralrimiva syl cmin cabs ccom cxp cres cbl crp
      wn cme eqid remet simprrl ad2antrr sylbi simprd iccssre reconnlem3 sseldd
      simpr resubcl wceq disjdif wi 3ad2ant2 eleq1 syl5ibcom jctild simp3 sseld
      syl6ibr syld n0i syl6 mt2i df-ne sylibr elicc2 mpbid simp3d leltne mpbird
      posdif elrp sylanbrc copn tgioo opni3 syl31anc cdiv caddc simprrr readdcl
      c2 ad2antrl biimpa lttrd 3jca cxr rexr 0re ltle sylc rpre rehalfcl adantl
      rpgt0 ltsubpos halfpos2 ltaddpos halfpos ltadd2 elioo2 simprl eleqtrrd cc
      bl2ioo simpld recn addid1 jctir jca32 simp2d jctil le2add leaddsub2 ltnle
      3syl eqbrtrrd breq1 notbid rcla4ev exp32 rexlimdv mpd rexnal syl6ib mt2d
      ex ) AECUHZJZUAUHZEKLZUAUVQFUHZGUHZUCMZUDZUEZAUWDNUFUWDVBUIUBUHUGUHZKLUBU
      WDUEUGNUJUKZUWEAUWANJZUWBNJZOZUWDUWCUFZUWAUWDJZUWGABCDFGHULZUWKAUVQUWCUMU
      NUWAUVQDUDZJZUWBBUHZDUDZJZUVQUWPUDZNDUOZUFZUKZDNUFZUVQUPUQURUSZJZUWPUXDJZ
      OOZOZUWAUWBKLZOZUWCDUFZOZUWAUVQJZUWAUWCJZOAUWLUXLUXMUXNUXBUXMUXGUXIUXKUWO
      UWRUXMUXAUWNUVQUWAUVQDUTVAVCVDUXLUWJUXIUXNUXLBCDFGUXLVEZULUXHUXIUXKVFUWHU
      WIUXIUXNUWAUWBVGVMPVHHUWAUVQUWCVIVJUGUBUWAUWBUWAUWDVKVLUWGUVTUAUWDUWGUVSU
      WDJOUVSUWDNQVNEKUGUBUWDUVSVOIVPVQVRAUVRUVTWFZUAUWDUJZUWEWFAUVRUXQAUVROZUW
      FUWBEVSMZQLZEUWFVTVSWANNWBWCZWDUSMZUVQUFZOZUGWEUJZUXQUXRUYAWGJZUXEUVRUXSW
      EJZUYEUYFUXRUYAUYAWHZWIUNAUXEUVRAUXLUXEHUXHUXEUXIUXKUXBUXCUXEUXFWJWKWLRAU
      VRWQZUXRUXSNJZSUXSQLZUYGAUYJUVRAUWIENJZUYJAUWHUWIUWMWMZAUWCNEAUWJUWCNUFUW
      MUWAUWBWNVRABCDEFGHIWOZWPZUWBEWRZPRUXREUWBQLZUYKUXRUYQUWBEUIZUXRUWBEWSZWF
      UYRUXRUYSDUWTUDZVBWSZDNWTUXRUYSEUYTJZVUAWFUXRUYSEDJZEUWTJZOVUBUXRUYSVUDVU
      CUXRUYSEUWSJZVUDUXRUYSUVREUWPJZOVUEUXRUYSVUFUVRAUYSVUFXAUVRAUWBUWPJZUYSVU
      FAUXLVUGHUXBVUGUXGUXIUXKUWRUWOVUGUXAUWQUWPUWBUWPDUTVAXBVDWLUWBEUWPXCXDRUY
      IXEEUVQUWPVIXHAVUEVUDXAUVRAUWSUWTEAUXLUXAHUXBUXAUXGUXIUXKUWOUWRUXAXFVDWLX
      GRXIAVUCUVRAUXLVUCHUXLUWCDEUXJUXKWQUXLBCDEFGUXOIWOWPWLRXEEDUWTVIXHUYTEXJX
      KXLUWBEXMXNAUYQUYRTZUVRAUYLUWIEUWBKLZVUHUYOUYMAUYLUWAEKLZVUIAEUWCJZUYLVUJ
      VUIUKZUYNAUWJVUKVULTUWMUWAUWBEXOVRXPZXQEUWBXRVLRXSAUYQUYKTZUVRAUYLUWIVUNU
      YOUYMEUWBXTPRXPUXSYAYBUGUVQUYAEUXSUXDUYAUYAYCUSZUYHVUOWHYDYEYFUXRUYDUXQUG
      WEUXRUWFWEJZUYDUXQUXRVUPUYDOZOZEUWFYKYGMZYHMZUWDJZVUTEKLZWFZUXQVURVUTUVQJ
      VUTUWCJZVVAVURUYBUVQVUTUXRVUPUXTUYCYIVURVUTEUWFVSMZEUWFYHMZUPMZUYBVURVUTV
      VGJZVUTNJZVVEVUTQLZVUTVVFQLZUKZVURVVIVVJVVKVURUYLVUSNJZVVIAUYLUVRVUQUYOWK
      ZVURUWFNJZVVMVUPVVOUXRUYDUWFUUAYLZUWFUUBVRZEVUSYJPZVURVVEEVUTVURUYLVVOVVE
      NJZVVNVVPEUWFWRPZVVNVVRVURSUWFQLZVVEEQLZVUPVWAUXRUYDUWFUUDYLVURVVOUYLVWAV
      WBTVVPVVNUWFEUUEPXPVURSVUSQLZEVUTQLZVUQVWCUXRVUPVWCUYDVUPVVOVWAOZVWCUWFYA
      ZVVOVWAVWCUWFUUFYMWLRUUCZVURVVMUYLVWCVWDTVVQVVNVUSEUUGPXPZYNVURVUSUWFQLZV
      VKVUPVWIUXRUYDVUPVWEVWIVWFVVOVWAVWIUWFUUHYMWLYLZVURVVMVVOUYLVWIVVKTVVQVVP
      VVNVUSUWFEUUIVLXPYOVURVVEYPJZVVFYPJZVVHVVLTVURVVSVWKVVTVVEYQVRVURVVFNJZVW
      LVURUYLVVOVWMVVNVVPEUWFYJPVVFYQVRVVEVVFVUTUUJPXSVURUYLVUPUYBVVGWSVVNUXRVU
      PUYDUUKEUWFUYAUYHUUNPUULWPVURVVDVVIUWAVUTKLZVUTUWBKLZUKZVURVVIVWNVWOVVRVU
      RUWASYHMZUWAVUTKAVWQUWAWSZUVRVUQAUWHUWAUUMJVWRAUWHUWIUWMUUOZUWAUUPUWAUUQU
      VEWKVURUWHSNJZOZUYLVVMOOVUJSVUSKLZOVWQVUTKLVURVXAUYLVVMVURUWHVWTAUWHUVRVU
      QVWSWKYRUURVVNVVQUUSVURVUJVXBAVUJUVRVUQAUYLVUJVUIVUMUUTWKVURVWTVVMOVWCVXB
      VURVVMVWTVVQYRUVAVWGSVUSYSYTVHUWASEVUSUVBYTUVFVURVWOVUSUXSKLZVURVVMUYJOVU
      SUXSQLVXCVURVVMUYJVVQVURUWIUYLUYJAUWIUVRVUQUYMWKZVVNUYPPZVHVURVUSUWFUXSVV
      QVVPVXEVWJUXRVUPUXTUYCWJYNVUSUXSYSYTVURUYLVVMUWIVWOVXCTVVNVVQVXDEVUSUWBUV
      CVLXSYOAVVDVWPTZUVRVUQAUWJVXFUWMUWAUWBVUTXOVRWKXSVUTUVQUWCVIYBVURVWDVVCVW
      HVURUYLVVIVWDVVCTVVNVVREVUTUVDPXPUXPVVCUAVUTUWDUVSVUTWSUVTVVBUVSVUTEKUVGU
      VHUVIPUVJUVKUVLUVPUVTUAUWDUVMUVNUVO $.
      $( [13-Jan-2014] $) $( [16-Aug-2009] $)
  $}

  ${
    $d m n r t ph $.  $d m n r t S $.  $d b m n r t $.  $d c m n r t $.
    $d m n r t u $.  $d m n r t v $.
    reconnlem5.1 $e |- ( ph <-> ( ( ( ( b e. ( u i^i A ) /\ c e. ( v i^i A ) /\
     ( u i^i v ) C_ ( RR \ A ) ) /\ ( A C_ RR /\ ( u e. ( topGen ` ran (,) ) /\
      v e. ( topGen ` ran (,) ) ) ) ) /\ b <_ c ) /\ ( b [,] c ) C_ A ) ) $.
    reconnlem5.2 $e |- S = sup ( ( u i^i ( b [,] c ) ) , RR , < ) $.
    $( Lemma for ~ reconn .  (Contributed by Jeff Hankins, 17-Aug-2009.) $)
    reconnlem5 $p |- ( ph -> -. S e. v ) $=
      ( vt vr cv wcel cr wss wa clt wbr adantr wi vn vm cabs cmin ccom cxp cres
      cbl cfv co crp wrex wn wral wex caddc w3a cicc cin c0 wne csup reconnlem2
      cle inss2 a1i cdif cioo inss1 sseli 3ad2ant1 ad3antrrr biid simplr lbicc2
      crn ctg 3expa syl2anc jca elin 3imtr4i iccsupr syl3anc iccssre reconnlem3
      syl sseldd rpre resubcl syl2an cc0 rpgt0 adantl wb ltsubpos syl2anr mpbid
      syl6breq suprlub syl12anc df-rex sseld syl5 simpr sylan syl6breqr ex jcad
      suprub wceq cc recn addid1 eqcomd ad2antrl simprl 0re jctir jca32 leltadd
      simprr sylc eqbrtrd syld adantrd 3jcad disjdif simprbi imp sylan2 adantrr
      a1d simp3 sylbi syl5bir exp3a eqid cxr rexr syl6ibr n0i eximdv syl5bi mpd
      syl6 bl2ioo eleq2d readdcl elioo2 bitrd anbi1d exbidv mpbird nss ralrimiv
      mt2i sylibr ralnex sylib remet simprrr ad2antrr copn tgioo opni2 mtand
      cme ) AEBLZMZEJLZUCUDUENNUFUGZUHUIUJZUVIOZJUKULZAUVNUMZJUKUNUVOUMAUVPJUKA
      UVKUKMZUVPAUVQPZKLZUVMMZUVSUVIMZUMZPZKUOZUVPUVRUWDUVSNMZEUVKUDUJZUVSQRZUV
      SEUVKUPUJZQRZUQZUWBPZKUOZUVRUWGKCLZFLZGLZURUJZUSZULZUWLUVRUWQNOUWQUTVAUAL
      UBLVDRUAUWQUNUBNULUQZUWFNMZUWFUWQNQVBZQRUWRAUWSUVQAUWNNMZUWONMZPZUWQUWPOZ
      UWNUWQMZUWSABCDFGHVCZUXEAUWMUWPVEZVFUWNUWMDUSZMZUWOUVIDUSMZUWMUVIUSZNDVGZ
      OZUQZDNOZUWMVHVPVQUIZMZUVIUXQMZPPZPZUWNUWOVDRZPZUWPDOZPZUWNUWMMZUWNUWPMZP
      AUXFUYEUYFUYGUXOUYFUXTUYBUYDUXJUXKUYFUXNUXIUWMUWNUWMDVIVJVKVLUYEUXDUYBUYG
      UYEBCDFGUYEVMVCUYAUYBUYDVNUXBUXCUYBUYGUWNUWOVOVRVSVTHUWNUWMUWPWAWBUBUAUWN
      UWOUWNUWQWCWDZSAENMZUVKNMZUWTUVQAUWPNEAUXDUWPNOUXGUWNUWOWEWGZABCDEFGHIWFW
      HZUVKWIZEUVKWJWKZUVRUWFEUXAQUVRWLUVKQRZUWFEQRZUVQUYOAUVKWMWNZUVQUYJUYIUYO
      UYPWOAUYMUYLUVKEWPWQWRIWSUBUAKUWQUWFWTXAUWRUVSUWQMZUWGPZKUOUVRUWLUWGKUWQX
      BUVRUYSUWKKUVRUYSUWJUWBUVRUYSUWEUWGUWIUYSUVSUWPMZUVRUWEUYRUYTUWGUWQUWPUVS
      UXHVJZSAUYTUWETUVQAUWPNUVSUYKXCZSXDUYSUWGTUVRUYRUWGXEVFUVRUYRUWIUWGUVRUYR
      UWEUVSEVDRZPZUWIAUYRVUDTUVQAUYRUWEVUCUYRUYTAUWEVUAVUBXDAUYRVUCAUYRPUVSUXA
      EVDAUWSUYRUVSUXAVDRUYHUBUAUWQUVSXJXFIXGXHXISUVRVUDUWIUVRVUDPZUVSUVSWLUPUJ
      ZUWHQUWEUVSVUFXKUVRVUCUWEVUFUVSUWEUVSXLMVUFUVSXKUVSXMUVSXNWGXOXPVUEUWEWLN
      MZPZUYIUYJPPVUCUYOPVUFUWHQRVUEVUHUYIUYJVUEUWEVUGUVRUWEVUCXQXRXSUVRUYIVUDA
      UYIUVQUYLSSUVRUYJVUDUVQUYJAUYMWNSXTVUEVUCUYOUVRUWEVUCYBUVRUYOVUDUYQSVTUVS
      WLEUVKYAYCYDXHYEYFYGUVRUYSUWBUVRUYSPZUWADUXMUSZUTXKZDNYHVUIUWAUVSVUJMZVUK
      UMVUIUWAUVSDMZUVSUXMMZPVULVUIUWAVUMVUNUVRUYRUWAVUMTUWGUVRUYRPVUMUWAUYRUVR
      UYTVUMVUAUVRUYTVUMAUYTVUMTUVQAUWPDUVSAUYCUYDHYIXCSYJYKYMYLUVRUYRUWAVUNTZU
      WGUVRUYRVUOUYRUVSUWMMZUVRVUOUWQUWMUVSUWMUWPVIVJUVRVUPUWAVUNVUPUWAPUVSUXLM
      ZUVRVUNUVSUWMUVIWAAVUQVUNTUVQAUXLUXMUVSAUYEUXNHUXOUXNUXTUYBUYDUXJUXKUXNYN
      VLYOXCSYPYQXDYJYLXIUVSDUXMWAUUAVUJUVSUUBUUFUUQXHXIUUCUUDUUEUVRUWCUWKKUVRU
      VTUWJUWBUVRUVTUVSUWFUWHVHUJZMZUWJUVRUVMVURUVSAUYIUVQUVMVURXKUYLEUVKUVLUVL
      YRZUUGXFUUHUVRUWTUWHNMZVUSUWJWOZUYNAUYIUYJVVAUVQUYLUYMEUVKUUIWKUWTUWFYSMU
      WHYSMVVBVVAUWFYTUWHYTUWFUWHUVSUUJWKVSUUKUULUUMUUNKUVMUVIUUOUURXHUUPUVNJUK
      UUSUUTAUVJPZUVLUVHMZUXSUVJUVOVVDVVCUVLVUTUVAVFAUXSUVJAUYEUXSHUYAUXSUYBUYD
      UXOUXPUXRUXSUVBUVCYOSAUVJXEJUVIUVLEUXQUVLUVLUVDUIZVUTVVEYRUVEUVFWDUVG $.
      $( [12-Jan-2014] $) $( [17-Aug-2009] $)
  $}

  ${
    $d b c u v x y A $.
    $( A subset of the reals is connected iff it has the interval property.
       (Contributed by Jeff Hankins, 15-Jul-2009.) $)
    reconn $p |- ( A C_ RR -> ( ( subSp ` <. A , ( topGen ` ran (,) ) >. ) e.
     Con <-> A. x e. A A. y e. A ( x [,] y ) C_ A ) ) $=
      ( vu vv vb vc cr wss wcel cv cicc co wral ex cin wi wa wceq sseq1d csubsp
      cioo crn ctg cfv cop ccon reconnlem1 c0 wne cdif w3a cun wn wex n0 cle wo
      inss2 sseli ssel2 sylan2 3ad2antr1 3ad2antr2 letric syl2anc ancoms opreq1
      wbr adantrr opreq2 rcla42v 3adant3 ad2antrr clt csup biid eqid reconnlem4
      syl2an reconnlem5 sstr2 adantl reconnlem3 ssel syl5com syl6ib mtord incom
      syld elun sseq1i biimpi 3com12 pm3.22 anim2i uncom sseq2i sylnib jaod mpd
      3anim3i com23 3exp com12 exlimiv sylbi com13 ralrimdvva wb retop uniretop
      3imp ctop connsub mpan sylibrd impbid ) CHIZCUBUCUDUEZUFUAUEUGJZAKZBKZLMZ
      CIZBCNACNZXSYAYFABCUHOXSYFDKZCPZUIUJZEKZCPZUIUJZYGYJPZHCUKZIZULZCYGYJUMZI
      ZUNZQZEXTNDXTNZYAXSYFYTDEXTXTYPYFXSYGXTJZYJXTJZRZRZYSYIYLYOYFUUEYSQQZYIFK
      ZYHJZFUOYLYOUUFQZQZFYHUPUUHUUJFYLUUHUUIYLGKZYKJZGUOUUHUUIQZGYKUPUULUUMGUU
      HUULUUIUUHUULYOUUFUUHUULYOULZUUEYFYSUUNUUEYFYSQZUUNUUERZUUGUUKUQVIZUUKUUG
      UQVIZURZUUOUUNXSUUSUUDXSUUNUUSXSUUNRUUGHJZUUKHJZUUSXSUULUUHUUTYOUUHXSUUGC
      JZUUTYHCUUGYGCUSUTZCHUUGVAVBVCXSUUHUULUVAYOUULXSUUKCJZUVAYKCUUKYJCUSUTZCH
      UUKVAVBVDUUGUUKVEVFVGVJUUPUUQUUOUURUUPUUQUUOUUPUUQRZYFUUGUUKLMZCIZYSUUNYF
      UVHQZUUEUUQUUHUULUVIYOUUHUVBUVDUVIUULUVCUVEYEUVHUUGYCLMZCIABUUGUUKCCYBUUG
      SYDUVJCYBUUGYCLVHTYCUUKSUVJUVGCYCUUKUUGLVKTVLVTVMVNUVFUVHYSUVFUVHRZYRYGUV
      GPHVOVPZYGJZUVLYJJZUVKEDCUVLFGUVKVQZUVLVRZVSUVKEDCUVLFGUVOUVPWAUVKYRUVLYQ
      JZUVMUVNURUVKYRUVGYQIZUVQUVHYRUVRQUVFUVGCYQWBWCUVKUVLUVGJUVRUVQUVKEDCUVLF
      GUVOUVPWDUVGYQUVLWEWFWJUVLYGYJWKWGWHOWJOUUNUULUUHYJYGPZYNIZULZXSUUCUUBRZR
      ZUURUUOQUUEUULUUHYOUWAYOUVTUULUUHYOUVTYMUVSYNYGYJWIWLWMXBWNUUDUWBXSUUBUUC
      WOWPUWAUWCRZUURUUOUWDUURRZYFUUKUUGLMZCIZYSUWAYFUWGQZUWCUURUULUUHUWHUVTUUL
      UVDUVBUWHUUHUVEUVCYEUWGUUKYCLMZCIABUUKUUGCCYBUUKSYDUWICYBUUKYCLVHTYCUUGSU
      WIUWFCYCUUGUUKLVKTVLVTVMVNUWEUWGYSUWEUWGRZCYJYGUMZIZYRUWJUWLYJUWFPHVOVPZY
      JJZUWMYGJZUWJDECUWMGFUWJVQZUWMVRZVSUWJDECUWMGFUWPUWQWAUWJUWLUWMUWKJZUWNUW
      OURUWJUWLUWFUWKIZUWRUWGUWLUWSQUWEUWFCUWKWBWCUWJUWMUWFJUWSUWRUWJDECUWMGFUW
      PUWQWDUWFUWKUWMWEWFWJUWMYJYGWKWGWHUWKYQCYJYGWQWRWSOWJOVTWTXAOXCXDXEXFXGXE
      XFXGXMXHXIXTXNJXSYAUUAXJXKDECXTHXLXOXPXQXR $.
      $( [15-Jul-2009] $)
  $}

  ${
    $d x y $.
    $( Corollary of ~ reconn .  The set of real numbers is connected.
       (Contributed by Jeff Hankins, 17-Aug-2009.) $)
    retopcon $p |- ( topGen ` ran (,) ) e. Con $=
      ( vx vy cr cioo crn ctg cfv cop csubsp ccon ctop wcel wceq retop uniretop
      subspid ax-mp cv wss wral cicc co iccssre rgen2a wb reconn mpbir eqeltrri
      ssid ) CDEFGZHIGZUJJUJKLUKUJMNUJCOPQUKJLZARZBRZUAUBCSZBCTACTZUOABCUMUNUCU
      DCCSULUPUECUIABCUFQUGUH $.
      $( [17-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( A closed interval is connected.  (Contributed by Jeff Hankins,
       17-Aug-2009.) $)
    iccconn $p |- ( ( A e. RR /\ B e. RR ) -> ( subSp ` <. ( A [,] B ) , (
     topGen ` ran (,) ) >. ) e. Con ) $=
      ( vx vy vz cr wcel wa cicc co cfv cv wss cle wbr w3a adantr elicc2 ex wb
      cioo crn ctg cop csubsp ccon wral wi 3ad2ant1 com12 simplll iccssre sseld
      ax-1 imp adantrr simp1 adantl biimpa simp2d simpr 3ad2antr2 letrd adantrl
      3ad2antr1 simpllr simp3 anim12i iccleub 3expa sylan 3jcad syl2anc 3imtr4d
      cxr rexr ssrdv ralrimivv reconn syl mpbird ) AFGZBFGZHZABIJZUAUBUCKUDUEKU
      FGZCLZDLZIJZWEMZDWEUGCWEUGZWDWJCDWEWEWDWGWEGZWHWEGZHZWJWDWNHZEWIWEWOELZFG
      ZWGWPNOZWPWHNOZPZWQAWPNOZWPBNOZPZWPWIGZWPWEGZWOWTWQXAXBWTWOWQWQWRWOWQUHWS
      WQWOUNUIUJWOWTXAWOWTHZAWGWPWBWCWNWTUKWOWGFGZWTWDWLXGWMWDWLXGWDWEFWGABULZU
      MUOUPZQWTWQWOWQWRWSUQURWOAWGNOZWTWDWLXJWMWDWLHXGXJWGBNOZWDWLXGXJXKPABWGRU
      SUTUPQWOWQWRWRWSWOWRVAVBVCSWOWTXBXFWPWHBWOWRWQWQWSWOWQVAVEWOWHFGZWTWDWMXL
      WLWDWMXLWDWEFWHXHUMUOVDZQWBWCWNWTVFWTWSWOWQWRWSVGURWOWHBNOZWTWDWMXNWLWDAV
      OGZBVOGZHWMXNWBXOWCXPAVPBVPVHXOXPWMXNABWHVIVJVKVDQVCSVLWOXGXLXDWTTXIXMWGW
      HWPRVMWDXEXCTWNABWPRQVNVQSVRWDWEFMWFWKTXHCDWEVSVTWA $.
      $( [17-Aug-2009] $)
  $}

  $( A connected topology is a topology.  (Contributed by FL, 22-Dec-2008.) $)
  contop $p |- ( J e. Con -> J e. Top ) $=
    ( ccon wcel ctop ccld cfv cin c0 cuni cpr wceq iscon2 simplbi ) ABCADCAAEFG
    HAIJKALM $.
    $( [14-Dec-2013] $) $( [22-Dec-2008] $)

  ${
    $d x y F $.
    $( A filter gives rise to a connected topology.  (Contributed by Jeff
       Hankins, 6-Dec-2009.)  (Revised by Mario Carneiro, 14-Dec-2013.) $)
    filcon $p |- ( F e. Fil -> ( F u. { (/) } ) e. Con ) $=
      ( vx vy wcel c0 cun cin cuni wceq wa wo wi wss wral cvv syl eleq1 wn elun
      ex cfil csn ccon ccld cfv cpr cv cdif ctop wal wb snex unexg mpan2 istopg
      uni0b ssun2 0ex snid sselii mpbiri sylbir a1i wex nss ssel2 adantll sylib
      simpll orcomd ord uniss ad2antlr uniun unisn uneq2i un0 3eqtrri syl6sseqr
      impr elssuni ad2antrl w3a eqid fillsb imp syl13anc exlimdv syl5bi pm2.61d
      elun1 alrimiv filint 3expa ralrimiva elsni ineq2 0ss mpbi syl6eq syl6eqel
      sseqin2 rgen ralun sylancl ineq1 df-ss a1d mpbir2and cldopn adantr orim2i
      ralrimiv sylbi cfbas filfbas fbncp sylan pm2.21d jaod adantl biimpri eqss
      ssdif0 syl2im orim12d syl5 expimpd jctil anbi12d syl5ibrcom filusb topcld
      syld 0cld jca impbid elin vex elpr eqeq2i orbi2i bitr4i 3bitr4g mpbird
      eqrdv iscon ) AUADZAEUBZFZUCDZUUJUUJUDUEZGZEUUJHZUFZIZUUHBUUMUUOUUHBUGZUU
      JDZUUQUULDZJZUUQEIZUUQAHZIZKZUUQUUMDUUQUUODZUUHUUTUVDUUHUURUUSUVDUUHUURJZ
      UUSUVBUUQUHZUUJDZUVDUUHUUSUVHLZUURUUHUUJUIDZUVIUUHUVJUUQUUJMZUUQHZUUJDZLZ
      BUJZUUQCUGZGZUUJDZCUUJNZBUUJNZUUHUUJODZUVJUVOUVTJUKUUHUUIODUWAEULAUUIUAOU
      MUNBCOUUJUOPUUHUVNBUUHUVKUVMUUHUVKJZUUQUUIMZUVMUWCUVMLUWBUWCUVLEIZUVMUUQU
      PUWDUVMEUUJDZUUIUUJEUUIAUQEURUSUTZUVLEUUJQVAVBVCUWCRUVPUUQDZUVPUUIDZRZJZC
      VDUWBUVMCUUQUUIVEUWBUWJUVMCUWBUWJUVMUWBUWJJZUVLADZUVMUWKUUHUVPADZUVLUVBMZ
      UVPUVLMZUWLUUHUVKUWJVIUWBUWGUWIUWMUWBUWGJZUWHUWMUWPUWMUWHUWPUVPUUJDZUWMUW
      HKUVKUWGUWQUUHUUQUUJUVPVFVGUVPAUUISVHVJVKVTUWKUVLUUNUVBUVKUVLUUNMUUHUWJUU
      QUUJVLVMUUNUVBUUIHZFUVBEFUVBAUUIVNUWREUVBEURVOVPUVBVQVRZVSUWGUWOUWBUWIUVP
      UUQWAWBUUHUWMUWNUWOWCUWLUVPUVLAUVBUVBWDZWEWFWGUVLAUUIWKPTWHWIWJTWLUUHUVSB
      ANUVSBUUINUVTUUHUVSBAUUHUUQADZJZUVRCANUVRCUUINUVSUXBUVRCAUUHUXAUWMUVRUUHU
      XAUWMWCUVQADUVRUUQUVPAWMUVQAUUIWKPWNWOUVRCUUIUWHUVPEIZUVRUVPEWPUXCUVQEUUJ
      UXCUVQUUQEGZEUVPEUUQWQEUUQMUXDEIUUQWREUUQXBWSWTUWFXAPXCUVRCAUUIXDXEWOUVSB
      UUIUUQUUIDZUVAUVSUUQEWPZUVAUVRCUUJUVAUVRUWQUVAUVQEUUJUVAUVQEUVPGZEUUQEUVP
      XFEUVPMUXGEIUVPWREUVPXGWSWTUWFXAXHXMPXCUVSBAUUIXDXEXIZUVJUUSUVHUUQUUJUVBU
      WSXJTPXKUVHUVGADZUVGEIZKZUVFUVDUVHUXIUVGUUIDZKUXKUVGAUUISUXLUXJUXIUVGEWPX
      LXNUVFUXIUVAUXJUVCUUHUURUXIUVALZUURUXAUXEKUUHUXMUUQAUUISUUHUXAUXMUXEUUHUX
      AUXMUXBUXIUVAUUHAXODUXAUXIRAXPUUQUVBAXQXRXSTUXEUXMLUUHUXEUVAUXIUXFXHVCXTW
      IWFUVFUUQUVBMZUXJUVBUUQMZUVCUURUXNUUHUURUUQUUNUVBUUQUUJWAUWSVSYAUXOUXJUVB
      UUQYDYBUXNUXOUVCUVCUXNUXOJUUQUVBYCYBTYEYFYGYNYHUUHUVAUUTUVCUUHUUTUVAUWEEU
      ULDZJUUHUXPUWEUUHUVJUXPUXHUUJYOPUWFYIUVAUURUWEUUSUXPUUQEUUJQUUQEUULQYJYKU
      UHUUTUVCUVBUUJDZUVBUULDZJUUHUXQUXRUUHUVBADUXQAUVBUWTYLUVBAUUIWKPUUHUVJUXR
      UXHUUJUVBUWSYMPYPUVCUURUXQUUSUXRUUQUVBUUJQUUQUVBUULQYJYKXTYQUUQUUJUULYRUV
      EUVAUUQUUNIZKUVDUUQEUUNBYSYTUVCUXSUVAUVBUUNUUQUWSUUAUUBUUCUUDUUFUUHUVJUUK
      UUPUKUXHUUJUUGPUUE $.
      $( [14-Dec-2013] $) $( [6-Dec-2009] $)
  $}


