$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Connectedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Con $.
  $( Extend class notation with the the class of all connected topologies. $)
  ccon $a class Con $.

  $( Topologies are connected when only ` (/) ` and ` U. j ` are both open and
     closed. $)
  df-con $a |- Con = { j e. Top | ( j i^i ( Clsd ` j ) ) = { (/) , U. j } } $.

  ${
    $d J j $.
    $( The predicate ` J ` is a connected topology .  (Contributed by FL,
       17-Nov-2008.) $)
    iscon $p |- ( J e. Top -> ( J e. Con <-> ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj ccon wcel ctop ccld cfv cin c0 cuni cpr wceq cv id fveq2 ineq12d
      unieq preq2d eqeq12d df-con elrab2 baib ) ACDAEDAAFGZHZIAJZKZLZBMZUHFGZHZ
      IUHJZKZLUGBAECUHALZUJUDULUFUMUHAUIUCUMNUHAFOPUMUKUEIUHAQRSBTUAUB $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)

    $( The predicate ` J ` is a connected topology .  (Contributed by FL,
       17-Nov-2008.) $)
    iscon2 $p |- ( J e. Con <-> ( J e. Top /\ ( J i^i ( Clsd ` J ) ) =
      { (/) , U. J } ) ) $=
      ( vj cv ccld cfv cin c0 cuni cpr wceq ctop ccon id fveq2 ineq12d unieq
      preq2d eqeq12d df-con elrab2 ) BCZUADEZFZGUAHZIZJAADEZFZGAHZIZJBAKLUAAJZU
      CUGUEUIUJUAAUBUFUJMUAADNOUJUDUHGUAAPQRBST $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)

  $}

  ${
    $d J x y $.  $d X y $.
    usinuniop.1 $e |- X = U. J $.
    $( If a topology is connected, its underlying set can't be partitioned into
       two non empty non-overlapping open sets.  (Contributed by FL,
       17-Nov-2008.) $)
    usinuniop $p |- ( J e. Con -> A. x e. J A. y e. J
       ( ( x =/= (/) /\ y =/= (/) /\ ( x i^i y ) = (/) )
         -> X =/= ( x u. y ) ) ) $=
      ( ccon wcel cv c0 wne cin wceq w3a cun wi wa cdif wn ctop ccld cfv cuni
      cpr iscon2 wo opncld eleq1 elin eleq2 biimpd visset elpr olc a1i com13
      eqcomi eqeq2i eqeq2 wss eltopss wal dfss2 cab df-dif eqtr wb abeq2
      pm4.71 albii 19.26 imim1 pm2.01 syl6 impcom alimi eq0 sylibr sylbir ex
      sylbi syl eqcoms mpi com12 imp orcd syl6bi jaoi syl6com com24 expcom
      pm2.43a com3l mpancom com14 imp41 nne orbi12i ianor con2d sylanb 3adant3
      elssuni eqcom sseq2i uneqdifeq syl5bb 3ad2ant3 syl5com ad2antrl
      necon3abid mpbird exp31 r19.21aivv ) CFGZAHZIJZBHZIJZXPXRKILZMZDXPXRNZJZO
      ABCCXOXPCGZXRCGZPZYAYCXOYFPZYAPZYCDXPQZXRLZRZYAYGYKXQXSYGYKOXTYGXQXSPZYKX
      OCSGZCCTUAZKZICUBZUCZLZPZYFYLYKOCUDYSYFPZYJYLYTYJYLRZYTYJPZXQRZXSRZUEZUUA
      UUBXPILZXRILZUEZUUEYMYRYFYJUUHYFYRYMYJUUHOZYEYDYRYMUUIOOYMYDYRYEUUIYMYDYR
      YEUUIOOZYIYNGZYMYDPZUUJXPCDEUFUUKUULPYJYEYRUUHUUKUULYJYEYRUUHOZOZOYJUUKUU
      LUUNUUKYJUULUUNOZYJUUKXRYNGZYJUUOOYIXRYNUGUUPYEUULYJUUMYEUUPUULYJUUMOOZYE
      UUPPXRYOGZUUQXRCYNUHUURYRYJUULUUHYRUURXRYQGZYJUULUUHOZOZYRUURUUSYOYQXRUIU
      JUUSUUGXRYPLZUEUVAXRIYPBUKULUUGUVAUVBUULYJUUGUUHYJUUGUUHOZOUULUVCYJUUGUUF
      UMUNUNUOUVBXRDLZUVAYPDXRDYPEUPZUQUVDYJYIDLZUUTXRDYIURUULUVFUUHUULXPDUSZUV
      FUUHOXPCDEUTUVGUVFUUHUVGUVFPUUFUUGUVGUVFUUFUVGXRXPGZXRDGZOZBVAZUVFUUFOBXP
      DVBUVFUVKUUFUVFYIUVIUVHRZPZBVCZLZUVKUUFOZBDXPVDUVOUVPODYIDYILZUVOUVPUVQUV
      OPDUVNLZUVPDYIUVNVEUVRUVIUVMVFZBVAZUVPUVMBDVGUVTUVIUVLOZBVAZUVPUWAUVSBUVI
      UVLVHVIUWBUVKUUFUWBUVKPUWAUVJPZBVAZUUFUWAUVJBVJUWDUVLBVAUUFUWCUVLBUVJUWAU
      VLUVJUWAUVHUVLOUVLUVHUVIUVLVKUVHVLVMVNVOBXPVPVQVRVSVRVTWAVSWBWCWDVTWEWFVS
      WAWDWGVTWHVTWIWJVRWKWJWGWLWMWEWJWNVSWOVNUOWPUUCUUFUUDUUGXPIWQXRIWQWRVQXQX
      SWSVQVSWTXAWDXBVNYHYJDYBYGYADYBLZYJVFZYDYAUWFOXOYEYDXPYPUSZYAUWFXPCXCXTXQ
      UWGUWFOXSUWGXTUWFUWEYBDLZUWGXTPYJDYBXDUWGUVGXTUWHYJVFYPDXPUVEXEXPXRDXFXAX
      GWKXHXIXJWEXKXLXMXN $.
      $( [1-Dec-2010] $) $( [16-Nov-2008] $)
  $}

  $( A connected topology is a topology.  (Contributed by FL, 22-Dec-2008.) $)
  contop $p |- Con C_ Top $=
    ( vx ccon ctop cv wcel ccld cfv cin c0 cuni cpr wceq iscon2 simplbi ssriv
    ) ABCADZBEPCEPPFGHIPJKLPMNO $.
    $( [1-Dec-2010] $) $( [22-Dec-2008] $)


