$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Filters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Fil $. $( The class of all filters. $)

  $( Extend class notation with the class of all filters. $)
  cfil $a class Fil $.

  ${
    $d f x y $.
    $( The class of all filters.  Bourbaki TG I.36 def. 1 axioms FI, FIIa,
       FIIb, FIII. Filters are used to define the concept of limit in the
       general case.  It's a generalization of the idea of neighborhoods.
       Suppose you are in ` RR ` .  With neighborhoods you can express the idea
       of a variable that tends to a specific number but you can't express the
       idea of a variable that tends to infinity.  Filters relax the "axioms"
       of neighborhoods and then succeed in expressing the idea of something
       that tends to infinity.  Filters were invented by Cartan in 1937 and
       made famous by Bourbaki in his treatise.  A notion similar to the notion
       of filter is the concept of net invented by Moore and Smith in 1922.
       (Contributed by FL, 20-Jul-2007.) $)
    df-fil $a |- Fil = { f e. fBas |
      A. x e. ~P U. f ( ( f i^i ~P x ) =/= (/) -> x e. f ) } $.
  $}

  ${
    $d F f x $.  $d X f x $.
    isfil.1 $e |- X = U. F $.
    $( The predicate "is a filter."  (Contributed by FL, 20-Jul-2007.) $)
    isfil $p |- ( F e. Fil <-> ( F e. fBas /\
      A. x e. ~P X ( ( F i^i ~P x ) =/= (/) -> x e. F ) ) ) $=
      ( vf cv cpw cin c0 wne wcel cuni wral cfbas cfil wceq unieq syl6eqr pweq
      wi syl ineq1 neeq1d eleq2 imbi12d raleqbidv df-fil elrab2 ) EFZAFZGZHZIJZ
      UJUIKZTZAUILZGZMBUKHZIJZUJBKZTZACGZMEBNOUIBPZUOVAAUQVBVCUPCPUQVBPVCUPBLCU
      IBQDRUPCSUAVCUMUSUNUTVCULURIUIBUKUBUCUIBUJUDUEUFAEUGUH $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d x F $.
    $( A filter is a filter base.  (Contributed by Jeff Hankins,
       2-Sep-2009.) $)
    filfbas $p |- ( F e. Fil -> F e. fBas ) $=
      ( vx cfil wcel cfbas cv cpw cin c0 wne wi cuni wral eqid isfil simplbi )
      ACDAEDABFZGHIJQADKBALZGMBARRNOP $.
      $( [27-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d F x y $.
    $( The empty set doesn't belong to a filter.  (Contributed by FL,
       20-Jul-2007.) $)
    filesn $p |- ( F e. Fil -> -. (/) e. F ) $=
      ( cfil wcel cfbas c0 wn filfbas 0nelfb syl ) ABCADCEACFAGAHI $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d B x $.  $d F x $.  $d X x $.
    fillsb.1 $e |- X = U. F $.
    $( A filter is closed under taking supersets.  (Contributed by FL,
       20-Jul-2007.) $)
    fillsb $p |- ( F e. Fil ->
                   ( ( A e. F /\ B C_ X /\ A C_ B ) -> B e. F ) ) $=
      ( vx cfil wcel wss w3a cpw cin c0 wne wa cvv cuni uniexg biimpar wi simp2
      syl5eqel elpw2g syl2an simp1 3adant2 elin sylanbrc ne0i syl adantl jca cv
      elpwg wral cfbas isfil simprbi wceq pweq ineq2d eleq1 imbi12d imp32 sylan
      neeq1d rcla4cv syldan ex ) CGHZACHZBDIZABIZJZBCHZVJVNBDKZHZCBKZLZMNZOZVOV
      JVNOVQVTVJDPHZVLVQVNVJDCQPECGRUBVKVLVMUAWBVQVLBDPUCSUDVNVTVJVNAVSHZVTVNVK
      AVRHZWCVKVLVMUEVKVMWDVLVKWDVMABCUNSUFACVRUGUHVSAUIUJUKULVJCFUMZKZLZMNZWEC
      HZTZFVPUOZWAVOVJCUPHWKFCDEUQURWKVQVTVOWJVTVOTFBVPWEBUSZWHVTWIVOWLWGVSMWLW
      FVRCWEBUTVAVFWEBCVBVCVGVDVEVHVI $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.
    $( A filter is closed under taking intersections.  (Contributed by FL,
       20-Jul-2007.) $)
    filint $p |- ( ( F e. Fil /\ A e. F /\ B e. F ) -> ( A i^i B ) e. F ) $=
      ( vx cfil wcel w3a cv cin wss wrex cfbas filfbas fbasssin syl3an1 wi cuni
      inss1 elssuni syl5ss wa eqid fillsb 3expd imp rexlimdv sylan2 3adant3 mpd
      com23 ) CEFZACFZBCFZGDHZABIZJZDCKZUOCFZUKCLFULUMUQCMDABCNOUKULUQURPZUMULU
      KUOCQZJZUSULUOAUTABRACSTUKVAUAUPURDCUKVAUNCFZUPURPZPUKVBVAVCUKVBVAUPURUNU
      OCUTUTUBUCUDUJUEUFUGUHUI $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d F x $.  $d X x $.
    filusb.1 $e |- X = U. F $.
    $( The underlying set belongs to the filter.  (Contributed by FL,
       20-Jul-2007.) $)
    filusb $p |- ( F e. Fil -> X e. F ) $=
      ( vx cfil wcel c0 wne cfbas filfbas fbasne0 syl cv wex n0 wa wss cuni mpd
      wi elssuni syl6sseqr adantl ssid fillsb 3expd imp mpi ex exlimdv syl5bi )
      AEFZAGHZBAFZULAIFUMAJAKLUMDMZAFZDNULUNDAOULUPUNDULUPUNULUPPZUOBQZUNUPURUL
      UPUOARBUOAUACUBUCUQBBQZURUNTZBUDULUPUSUTTULUPUSURUNUOBABCUEUFUGUHSUIUJUKS
      $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d F x y z $.  $d X x y $.
    isfil2.1 $e |- X = U. F $.
    $( Derive the standard axioms of a filter.  (Contributed by Mario Carneiro,
       27-Nov-2013.) $)
    isfil2 $p |- ( F e. Fil <-> ( ( -. (/) e. F /\ X e. F ) /\
        A. x e. ~P X ( E. y e. F y C_ x -> x e. F ) /\
        A. x e. F A. y e. F ( x i^i y ) e. F ) ) $=
      ( vz wcel c0 wa cv wss wrex wi cpw wral cin w3a elpwi cvv ralimi cfil jca
      filesn filusb fillsb 3expd com23 imp rexlimdv sylan2 ralrimiva ralrimivva
      wn filint 3expb 3jca cfbas wne wnel simp1r pwuni eleq1i pwexg sylbi ssexg
      cuni sylancr syl ne0i simp1l df-nel sylibr sseq1 rcla4ev 3ad2ant3 isfbas2
      ssid mpan2 biimpar syl13anc wex elin anim2i eximi df-rex 3imtr4i 3ad2ant2
      n0 imim1i isfil sylanbrc impbii ) CUAGZHCGUMZDCGZIZBJZAJZKZBCLZWRCGZMZADN
      ZOZWRWQPZCGZBCOZACOZQZWMWPXDXHWMWNWOCUCCDEUDUBWMXBAXCWRXCGWMWRDKZXBWRDRWM
      XJIWSXABCWMXJWQCGZWSXAMZMWMXKXJXLWMXKXJWSXAWQWRCDEUEUFUGUHUIUJUKWMXFABCCW
      MXAXKXFWRWQCUNUOULUPXICUQGZCWRNZPZHURZXAMZAXCOZWMXICSGZCHURZHCUSZFJZXEKZF
      CLZBCOZACOZXMXIWOXSWNWOXDXHUTZWOCCVFZNZKYISGZXSCVAWOYHCGYJDYHCEVBYHCVCVDC
      YISVEVGVHXIWOXTYGCDVIVHXIWNYAWNWOXDXHVJHCVKVLXHWPYFXDXGYEACXFYDBCXFXEXEKZ
      YDXEVQYCYKFXECYBXEXEVMVNVRTTVOXSXMXTYAYFQABFSCVPVSVTXDWPXRXHXBXQAXCXPWTXA
      WQXOGZBWAXKWSIZBWAXPWTYLYMBYLXKWQXNGZIYMWQCXNWBYNWSXKWQWRRWCVDWDBXOWHWSBC
      WEWFWITWGACDEWJWKWL $.
      $( [27-Nov-2013] $)
  $}

  ${
    $d x y A $.  $d z A $.  $d x y F $.  $d y z F $.  $d x y ph $.
    $d y z ph $.  $d y ps $.  $d y B $.
    isfild.1 $e |- ( ph -> ( x e. F <-> ( x C_ A /\ ps ) ) ) $.
    isfild.2 $e |- ( ph -> A e. _V ) $.
    $( Lemma for ~ isfild . $)
    isfildlem $p |- ( ph -> ( B e. F <-> ( B C_ A /\ [ B / x ] ps ) ) ) $=
      ( vy cvv wcel wss wsbc wa wi wb cv wceq eleq1 ax-17 elisset a1i ssexg syl
      expcom adantrd sseq1 dfsbcq anbi12d bibi12d imbi2d hbs1 hban hbbi sbceq1a
      hbim visset vtoclf vtoclg com12 pm5.21ndd ) AEJKZEFKZEDLZBCEMZNZVCVBOAEFU
      AUBAVDVBVEADJKZVDVBOHVDVGVBEDJUCUEUDUFVBAVCVFPZAIQZFKZVIDLZBCVIMZNZPZOZAV
      HOIEJVIERZVNVHAVPVJVCVMVFVIEFSVPVKVDVLVEVIEDUGBCVIEUHUIUJUKACQZFKZVQDLZBN
      ZPZOVOCVIAVNCACTVJVMCVJCTVKVLCVKCTBCIULUMUNUPIUQVQVIRZWAVNAWBVRVJVTVMVQVI
      FSWBVSVKBVLVQVIDUGBCVIUOUIUJUKGURUSUTVA $.
      $( [1-Dec-2013] $)

    isfild.3 $e |- ( ph -> [ A / x ] ps ) $.
    $( Base set of a filter of the form ` { x e. ~P A | ph } ` .  (Contributed
       by Mario Carneiro, 14-Dec-2013.) $)
    isfildu $p |- ( ph -> A = U. F ) $=
      ( cuni wcel wss wsbc wa ssid jctil isfildlem mpbird elssuni syl cv syl6bi
      wral simpl ralrimiv unissb sylibr eqssd ) ADEIZADEJZDUHKAUIDDKZBCDLZMAUKU
      JHDNOABCDDEFGPQDERSACTZDKZCEUBUHDKAUMCEAULEJUMBMUMFUMBUCUAUDCEDUEUFUG $.
      $( [14-Dec-2013] $)

    isfild.4 $e |- ( ph -> -. [ (/) / x ] ps ) $.
    isfild.5 $e |- ( ( ph /\ y C_ A /\ z C_ y ) ->
                     ( [ z / x ] ps -> [ y / x ] ps ) ) $.
    isfild.6 $e |- ( ( ph /\ y C_ A /\ z C_ A ) ->
        ( ( [ y / x ] ps /\ [ z / x ] ps ) -> [ ( y i^i z ) / x ] ps ) ) $.
    $( Sufficient condition for a set of the form ` { x e. ~P A | ph } ` to be
       a filter.  (Contributed by Mario Carneiro, 1-Dec-2013.) $)
    isfild $p |- ( ph -> F e. Fil ) $=
      ( c0 wcel wa wss wi wsbc isfildlem wn cuni cv wrex cpw wral cin w3a simpr
      cfil syl6bi mtod visset elpw biimpri adantr ssrdv sylib ssid jctil mpbird
      sspwuni elssuni syl eqssd eqeltrd jca elpwi sseq2d simp2 a1d jcad adantld
      wb 3ad2ant1 3imtr4d 3expa impancom rexlimdva sylbid syl5 ralrimiv ssinss1
      ex ad2antrr a1i 3expb expimpd syl5bi anbi12d ralrimivv 3jca isfil2 sylibr
      an4 eqid ) ANGOZUAZGUBZGOZPZEUCZDUCZQZEGUDXCGOZRZDWSUEZUFZXCXBUGZGOZEGUFD
      GUFZUHGUJOAXAXHXKAWRWTAWQBCNSZKAWQNFQZXLPXLABCFNGHITXMXLUIUKULAWSFGAWSFAG
      FUEZQWSFQACGXNACUCZGOXOFQZBPXOXNOZHXPXQBXQXPXOFCUMUNUOUPUKUQGFVBURAFGOZFW
      SQAXRFFQZBCFSZPAXTXSJFUSUTABCFFGHITVAZFGVCVDVEZYAVFVGAXFDXGXCXGOXCWSQZAXF
      XCWSVHAYCXCFQZXFAWSFXCYBVIAYDXFAYDPZXDXEEGYEXDXBGOZXEAYDXDYFXERAYDXDUHZXB
      FQZBCXBSZPZYDBCXCSZPZYFXEYGYIYLYHYGYIYDYKYGYDYIAYDXDVJVKLVLVMAYDYFYJVNXDA
      BCFXBGHITZVOAYDXEYLVNXDABCFXCGHITZVOVPVQVRVSWDVTWAWBAXJDEGGAYLYJPZXIFQZBC
      XISZPXEYFPXJAYOYPYQYOYPRAYDYPYKYJXCXBFWCWEWFYOYDYHPZYKYIPZPAYQYDYKYHYIWOA
      YRYSYQAYDYHYSYQRMWGWHWIVLAXEYLYFYJYNYMWJABCFXIGHITVPWKWLDEGWSWSWPWMWN $.
      $( [1-Dec-2013] $)
  $}

  ${
    $d F x y $.
    $( A filter is closed under taking intersections.  (Contributed by Mario
       Carneiro, 27-Nov-2013.) $)
    filfi $p |- ( F e. Fil -> ( fi ` F ) = F ) $=
      ( vx vy cfil wcel cv cin wral cfi cfv wceq filint 3expib ralrimivv inficl
      mpbid ) ADEZBFZCFZGAEZCAHBAHAIJAKQTBCAAQRAESAETRSALMNBCADOP $.
      $( [27-Nov-2013] $)
  $}

  $( The intersection of two elements of a filter can't be empty.  (Contributed
     by FL, 19-Sep-2007.) $)
  fipfil $p |- ( F e. Fil -> ( ( A e. F /\ B e. F ) ->
    ( A i^i B ) =/= (/) ) ) $=
    ( cfil wcel wa cin c0 wceq wn filint 3expb filesn adantr nelneq syl11anc ex
    wne df-ne syl6ibr ) CDEZACEZBCEZFZABGZHIJZUEHRUAUDUFUAUDFUECEZHCEJZUFUAUBUC
    UGABCKLUAUHUDCMNUEHCOPQUEHST $.
    $( [1-Dec-2010] $) $( [16-Sep-2007] $)

  $( A filter has the finite intersection property.  Bourbaki TG I.36 note of
     def. 1.  (Contributed by FL, 2-Sep-2007.) $)
  fipfil2 $p |- ( F e. Fil -> ( ( A C_ F /\ A =/= (/) /\ A e. Fin ) ->
                  |^| A =/= (/) ) ) $=
    ( cfil wcel wss c0 wne cfn w3a cint cfi cfv elfir ex filfi eleq2d sylibd wn
    wceq filesn eleq1 notbid syl5ibrcom necon2ad syld ) BCDZABEAFGAHDIZAJZBDZUH
    FGUFUGUHBKLZDZUIUFUGUKABCMNUFUJBUHBOPQUFUIUHFUFUIRUHFSZFBDZRBTULUIUMUHFBUAU
    BUCUDUE $.
    $( [27-Nov-2013] $) $( [20-Sep-2007] $)

  $( The empty set is not a filter.  Bourbaki TG I.36 def 1 note.  (Contributed
     by FL, 31-Oct-2007.) $)
  emnfil $p |- ( F e. Fil -> F =/= (/) ) $=
    ( cfil wcel cuni c0 wne eqid filusb ne0i syl ) ABCADZACAEFAKKGHAKIJ $.
    $( [27-Nov-2013] $) $( [30-Oct-2007] $)

  ${
    $d F x y $.  $d G x y $.
    $( The intersection of two filters is a filter.  Use ~ fiint to extend this
       property to the intersection of a finite set of filters.  Bourbaki TG
       I.36 par. 3.  (Contributed by FL, 19-Sep-2007.) $)
    infil $p |- ( ( F e. Fil /\ G e. Fil /\ ( F i^i G ) =/= (/) ) ->
      ( F i^i G ) e. Fil ) $=
      ( vy vx cfil wcel cin c0 w3a wn cuni wa wss wral sseli cvv syl eqid imp
      wi wne wrex cpw filesn 3ad2ant1 inss1 nsyl ssexg mpan uniexg elpwg mpbiri
      ssid simpl1 simpr2 simpr1 elpwi uniss ax-mp sstr mpan2 3syl simpr3 fillsb
      cv syl13anc simpl2 inss2 sylanbrc ex 3expd rexlimdv ralrimiva wex elssuni
      elin ancli eximi df-rex 3imtr4i 3ad2ant3 wceq sseq2 rexbidv eleq1 imbi12d
      n0 rcla4v syl3c simpl anim12i filint 3expb syl2an ralrimivva 3adant3 3jca
      jca simpr isfil2 sylibr ) AEFZBEFZABGZHUAZIZHXDFZJZXDKZXDFZLZCVEZDVEZMZCX
      DUBZXMXDFZTZDXIUCZNZXMXLGZXDFZCXDNDXDNZIXDEFXFXKXSYBXFXHXJXFHAFZXGXBXCYCJ
      XEAUDUEXDAHABUFZOUGXFXIXRFZXSXLXIMZCXDUBZXJXBXCYEXEXBXIPFZYEXBXDPFZYHXDAM
      ZXBYIYDXDAEUHUIXDPUJQYHYEXIXIMXIUMXIXIPUKULQUEXFXQDXRXFXMXRFZLXNXPCXDXFYK
      XLXDFZXNXPTTXFYKYLXNXPXFYKYLXNIZXPXFYMLZXMAFZXMBFZXPYNXBXLAFZXMAKZMZXNYOX
      BXCXEYMUNYNYLYQXFYKYLXNUOZXDAXLYDOZQYNYKXMXIMZYSXFYKYLXNUPZXMXIUQZUUBXIYR
      MZYSYJUUEYDXDAURUSXMXIYRUTVAVBXFYKYLXNVCZXBYQYSXNIYOXLXMAYRYRRVDSVFYNXCXL
      BFZXMBKZMZXNYPXBXCXEYMVGYNYLUUGYTXDBXLABVHZOZQYNYKUUBUUIUUCUUDUUBXIUUHMZU
      UIXDBMUULUUJXDBURUSXMXIUUHUTVAVBUUFXCUUGUUIXNIYPXLXMBUUHUUHRVDSVFXMABVPVI
      VJVKSVLVMZXEXBYGXCYLCVNYLYFLZCVNXEYGYLUUNCYLYFXLXDVOVQVRCXDWGYFCXDVSVTWAX
      QYGXJTDXIXRXMXIWBZXOYGXPXJUUOXNYFCXDXMXIXLWCWDXMXIXDWEWFWHWIWRUUMXBXCYBXE
      XBXCLZYADCXDXDUUPXPYLLZLXTAFZXTBFZYAUUPXBYOYQLUURUUQXBXCWJXPYOYLYQXDAXMYD
      OUUAWKXBYOYQUURXMXLAWLWMWNUUPXCYPUUGLUUSUUQXBXCWSXPYPYLUUGXDBXMUUJOUUKWKX
      CYPUUGUUSXMXLBWLWMWNXTABVPVIWOWPWQDCXDXIXIRWTXA $.
      $( [27-Nov-2013] $) $( [17-Sep-2007] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( A singleton is a filter.  Bourbaki TG I.36, example 1.  (Contributed by
       FL, 19-Sep-2007.) $)
    snfil $p |- ( ( A e. B /\ A =/= (/) ) -> { A } e. Fil ) $=
      ( vx vy wcel c0 wne wa cv wceq wss wb a1i cvv adantr wsbc eqsbc3 ax-mp wi
      w3a csn elsn eqimss pm4.71ri bitri elisset eqid mpbiri simpr necomd df-ne
      wn sylib 0ex mtbird sseq1 anbi2d eqss biimpri syl6bi com12 3adant1 visset
      3imtr4g cin ineq12 inidm syl6eq anbi12i inex1 3imtr4i isfild ) ABEZAFGZHZ
      CIZAJZCDCAAUAZVPVREZVPAKZVQHZLVOVSVQWACAUBVQVTVPAUCUDUEMVMANEVNABUFOVMVQC
      APZVNVMWBAAJAUGCAABQUHOVOVQCFPZFAJZVOFAGWDULVOAFVMVNUIUJFAUKUMWCWDLZVOFNE
      WEUNCFANQRMUOVODIZAKZVPWFKZTVQWFAJZVQCVPPZVQCWFPZWGWHVQWISVOVQWGWHHZWIVQW
      LWGAWFKZHZWIVQWHWMWGVPAWFUPUQWIWNWFAURUSUTVAVBVPNEWJVQLCVCCVPANQRZWFNEWKW
      ILDVCZCWFANQRZVDWKWJHZVQCWFVPVEZPZSVOWGVTTWIVQHZWSAJZWRWTXAWSAAVEAWFAVPAV
      FAVGVHWKWIWJVQWQWOVIWSNEWTXBLWFVPWPVJCWSANQRVKMVL $.
      $( [1-Dec-2013] $) $( [16-Sep-2007] $)
  $}

  ${
    $d x y z A $.  $d x y V $.
    $( A condition for a set to generate a filter base.  (Contributed by Jeff
       Hankins, 2-Sep-2009.) $)
    fsubbas $p |- ( A e. V -> ( ( fi ` A ) e. fBas <->
                    ( A =/= (/) /\ -. (/) e. ( fi ` A ) ) ) ) $=
      ( vz vx vy wcel cfi cfv cfbas c0 wne wn wa fbasne0 w3a cv wss wral sylibr
      cvv fi0 necon3bid biimpar sylan2 0nelfb adantl jca ex wnel biimpa 3adant3
      cin wrex simp3 df-nel fiin ssid sseq1 rcla4ev sylancl rgen2 a1i 3jca fvex
      wb isfbas2 ax-mp 3expib impbid ) ABFZAGHZIFZAJKZJVKFLZMZVJVLVOVJVLMVMVNVL
      VJVKJKZVMVKNVJVMVPVJAJVKJABUAUBZUCUDVLVNVJVKUEUFUGUHVJVMVNVLVJVMVNOZVPJVK
      UIZCPZDPZEPZULZQZCVKUMZEVKRDVKRZOZVLVRVPVSWFVJVMVPVNVJVMVPVQUJUKVRVNVSVJV
      MVNUNJVKUOSWFVRWEDEVKVKWAVKFWBVKFMWCVKFWCWCQZWEWAWBAUPWCUQWDWHCWCVKVTWCWC
      URUSUTVAVBVCVKTFVLWGVEAGVDDECTVKVFVGSVHVI $.
      $( [25-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x y z F $.
    $( A filter base has the finite intersection property.  (Contributed by
       Jeff Hankins, 2-Sep-2009.) $)
    fbasfip $p |- ( F e. fBas -> -. (/) e. ( fi ` F ) ) $=
      ( vy vz cfbas wcel c0 cfi cfv cv cint wceq cpw cfn wrex wa wss wn wi mtod
      cvv cin elin elpwi anim1i sylbi fbssint 3expb sylan2 0nelfb eleq1 biimpcd
      ad2antrr adantl ss0 adantrr sseq2 biimprcd ad2antll exp32 rexlimdv nrexdv
      nsyl mpd wb 0ex elfi mpan mtbird ) ADEZFAGHEZFBIZJZKZBALZMUAZNZVIVMBVOVIV
      KVOEZOZCIZVLPZCANZVMQZVQVIVKAPZVKMEZOZWAVQVKVNEZWDOWEVKVNMUBWFWCWDVKAUCUD
      UEVIWCWDWACVKAUFUGUHVRVTWBCAVRVSAEZVTWBVRWGVTOOVMVSFPZVRWGWHQVTVRWGOZVSFK
      ZWHWIWJFAEZVIWKQVQWGAUIULWGWJWKRVRWJWGWKVSFAUJUKUMSVSUNVBUOVTVMWHRVRWGVMW
      HVTFVLVSUPUQURSUSUTVCVAFTEVIVJVPVDVEBFATDVFVGVH $.
      $( [25-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d w x y z G $.  $d x y z F $.
    $( A helpful lemma for showing that certain sets generate filters.
       (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fbunfip $p |- ( ( F e. fBas /\ G e. fBas ) ->
                    ( -. (/) e. ( fi ` ( F u. G ) ) <->
                      A. x e. F A. y e. G ( x i^i y ) =/= (/) ) ) $=
      ( vz vw cfbas wcel wa c0 cfi wn cv wceq wrex wne wral wss wi syl wb necom
      cun cfv cin w3o elfiun notbid w3a 3ioran df-3an bitr2i df-ne bitri ralbii
      syl6bbr ralnex a1i fbasfip anim12i biantrurd bitr2d ssralv ralimdv sylan9
      ssfii ineq1 neeq1d ineq2 cbvral2v fbssfi r19.29 ss2in ss0 syl5com necon3d
      sseq2 syl6bi com13 imp rexlimivw impancom expimpd com12 syl2an an4s an32s
      ex ralrimivva syl5bi impbid 3bitrd ) CGHZDGHZIZJCDUCKUDHZLZJCKUDZHZLZJDKU
      DZHZLZIZJAMZBMZUEZNZBXAOZAWROZLZIZXGJPZBXAQZAWRQZXMBDQZACQZWOWQWSXBXJUFZL
      ZXLWOWPXRABJCDGGUGUHXSWTXCXKUIXLWSXBXJUJWTXCXKUKULUPWOXOXKXLXOXKUAWOXOXIL
      ZAWRQXKXNXTAWRXNXHLZBXAQXTXMYABXAXMJXGPYAXGJUBJXGUMUNUOXHBXAUQUNUOXIAWRUQ
      UNURWOXDXKWMWTWNXCCUSDUSUTVAVBWOXOXQWMXOXNACQZWNXQWMCWRRXOYBSCGVFXNACWRVC
      TWNXNXPACWNDXARXNXPSDGVFXMBDXAVCTVDVEXQEMZFMZUEZJPZFDQZECQZWOXOXMYFYCXFUE
      ZJPABEFCDXEYCNXGYIJXEYCXFVGVHXFYDNYIYEJXFYDYCVIVHVJWOYHXOWOYHIXMABWRXAWOX
      EWRHZXFXAHZIZYHXMWOYLIYHXMWMYJWNYKYHXMSZWMYJIYCXERZECOZYDXFRZFDOZYMWNYKIE
      XECVKFXFDVKYHYOYQIXMYHYOYQXMYHYOIYGYNIZECOYQXMSZYGYNECVLYRYSECYGYQYNXMYGY
      QIYFYPIZFDOYNXMSZYFYPFDVLYTUUAFDYFYPUUAYNYPYFXMYNYPYFXMSYNYPIZXGJYEJUUBYE
      XGRZXGJNZYEJNZYCXEYDXFVMUUDUUCYEJRUUEXGJYEVQYEVNVRVOVPWHVSVTWATWBWATWCWDW
      EWFVTWGWIWHWJWKWL $.
      $( [26-Nov-2013] $) $( [3-Sep-2009] $)
  $}

  ${
    $d f x F $.  $d f x X $.
    fgf.1 $e |- X = U. F $.
    $( The filter generating class gives a filter for every filter base.
       (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fgf $p |- ( F e. fBas -> ( filGen ` F ) = { x e. ~P X | ( F i^i ~P x ) =/=
    (/) } ) $=
      ( vf cfbas wcel cv cpw cin wne crab cvv cfg cfv wceq cuni uniexg syl5eqel
      c0 pwexg rabexg 3syl unieq syl6eqr pweq syl ineq1 neeq1d rabeqbidv fvmptg
      df-fg mpdan ) BFGZBAHIZJZTKZACIZLZMGZBNOUSPUNCMGURMGUTUNCBQZMDBFRSCMUAUQA
      URMUBUCEBEHZUOJZTKZAVBQZIZLUSFMNVBBPZVDUQAVFURVGVECPVFURPVGVEVACVBBUDDUEV
      ECUFUGVGVCUPTVBBUOUHUIUJEAULUKUM $.
      $( [26-Nov-2013] $) $( [3-Sep-2009] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y F $.  $d y X $.  $d x y $.
    elfg.1 $e |- X = U. F $.
    $( A condition for elements of a generated filter.  (Contributed by Jeff
       Hankins, 3-Sep-2009.) $)
    elfg $p |- ( F e. fBas -> ( A e. ( filGen ` F ) <-> ( A C_ X
    /\ E. x e. F x C_ A ) ) ) $=
      ( vy cfbas wcel cfg cfv cv cpw cin c0 wne wss wa cvv wb wex crab wrex fgf
      eleq2d wceq pweq ineq2d neeq1d elrab cuni uniexg syl5eqel elpw2g syl elin
      visset elpw anbi2i bitri exbii n0 df-rex 3bitr4i a1i anbi12d syl5bb bitrd
      ) CGHZBCIJZHBCFKZLZMZNOZFDLZUAZHZBDPZAKZBPZACUBZQZVHVIVOBFCDEUCUDVPBVNHZC
      BLZMZNOZQVHWAVMWEFBVNVJBUEZVLWDNWFVKWCCVJBUFUGUHUIVHWBVQWEVTVHDRHWBVQSVHD
      CUJRECGUKULBDRUMUNWEVTSVHVRWDHZATVRCHZVSQZATWEVTWGWIAWGWHVRWCHZQWIVRCWCUO
      WJVSWHVRBAUPUQURUSUTAWDVAVSACVBVCVDVEVFVG $.
      $( [1-Dec-2010] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.
    $( A filter base is a subset of its generated filter.  (Contributed by Jeff
       Hankins, 3-Sep-2009.) $)
    fbssfg $p |- ( F e. fBas -> F C_ ( filGen ` F ) ) $=
      ( vt vx cfbas wcel cfg cfv cv cuni wss wrex wa elssuni ssid sseq1 rcla4ev
      mpan2 jca eqid elfg syl5ibr ssrdv ) ADEZBAAFGZBHZAEZUEUDEUCUEAIZJZCHZUEJZ
      CAKZLUFUHUKUEAMUFUEUEJZUKUENUJULCUEAUIUEUEOPQRCUEAUGUGSTUAUB $.
      $( [1-Dec-2010] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x y F $.  $d t x y X $.
    fgbas.1 $e |- X = U. F $.
    $( The base set of a generated filter is the base set of the parent base.
       (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fgbas $p |- ( F e. fBas -> X = U. ( filGen ` F ) ) $=
      ( vt vx vy cfbas wcel cfg cfv cuni wss fbssfg uniss syl syl5eqss cv eluni
      wa wex wrex anbi2d ssel2 ancoms adantrr syl6bi exlimdv syl5bi ssrdv eqssd
      elfg ) AGHZBAIJZKZULBAKZUNCULAUMLUOUNLAMAUMNOPULDUNBDQZUNHUPEQZHZUQUMHZSZ
      ETULUPBHZEUPUMRULUTVAEULUTURUQBLZFQUQLFAUAZSZSVAULUSVDURFUQABCUKUBURVBVAV
      CVBURVAUQBUPUCUDUEUFUGUHUIUJ $.
      $( [1-Dec-2010] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.  $d t x G $.
    $( A bigger base generates a bigger filter. $)
    fgss $p |- ( ( F e. fBas /\ G e. fBas /\ F C_ G ) -> ( filGen ` F ) C_ (
    filGen ` G ) ) $=
      ( vt vx cfbas wcel wss w3a cfg cfv cv cuni wrex wa wi uniss sstr2 wb eqid
      elfg syl5com ssrexv anim12d 3ad2ant3 3ad2ant1 3ad2ant2 3imtr4d ssrdv ) AE
      FZBEFZABGZHZCAIJZBIJZULCKZALZGZDKUOGZDAMZNZUOBLZGZURDBMZNZUOUMFZUOUNFZUKU
      IUTVDOUJUKUQVBUSVCUKUPVAGUQVBABPUOUPVAQUAURDABUBUCUDUIUJVEUTRUKDUOAUPUPST
      UEUJUIVFVDRUKDUOBVAVASTUFUGUH $.
      $( [5-Sep-2009] $)
  $}

  ${
    $d t u v x y F $.  $d t u v x y G $.  $d t u v x y X $.  $d t u v x y Y $.
    $d t u v x y $.
    fbfgss.1 $e |- X = U. F $.
    fbfgss.2 $e |- Y = U. G $.
    $( A condition for a filter to be finer than another involving their filter
       bases.  (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fbfgss $p |- ( ( F e. fBas /\ G e. fBas /\ X = Y ) -> ( ( filGen ` F ) C_ (
    filGen ` G ) <-> A. x e. F E. y e. G y C_ x ) ) $=
      ( vt vu vv cfbas wcel wceq wss cv wrex wa wi elfg w3a cfg cfv wral fbssfg
      3ad2ant1 sseld ssel2 simpr syl6bi 3ad2ant2 syl5 exp3a syl5d sseq2 rexbidv
      ralrimdv rcla4v adantl simpll3 sseq2d sstr sseq1 rcla4ev a1d sylanr2 jcad
      biimpd exp45 rexlimdv syld ex com23 imp imp3a adantr 3imtr4d ssrdv impbid
      wb ) CLMZDLMZEFNZUAZCUBUCZDUBUCZOZBPZAPZOZBDQZACUDZWDWGWKACWDWICMWIWEMZWG
      WKWDCWEWIWAWBCWEOWCCUEUFUGWDWGWMWKWGWMRWIWFMZWDWKWEWFWIUHWBWAWNWKSWCWBWNW
      IFOZWKRWKBWIDFHTWOWKUIUJUKULUMUNUQWDWLWGWDWLRZIWEWFWPIPZEOZJPZWQOZJCQZRZW
      QFOZKPZWQOZKDQZRZWQWEMZWQWFMZWPWRXAXGWPXAWRXGWPWTWRXGSZJCWDWLWSCMZWTXJSZS
      WDXKWLXLWDXKWLXLSWDXKRZWLWHWSOZBDQZXLXKWLXOSWDWKXOAWSCWIWSNWJXNBDWIWSWHUO
      UPURUSXMXNXLBDXMWHDMZXNWTXJXMXPXNWTRZRZRZWRXCXFXSWRXCXSEFWQWAWBWCXKXRUTVA
      VHXQXMXPWHWQOZWRXFSWHWSWQVBXMXPXTRZRXFWRYAXFXMXEXTKWHDXDWHWQVCVDUSVEVFVGV
      IVJVKVLVMVNVJVMVOWDXHXBVTZWLWAWBYBWCJWQCEGTUFVPWDXIXGVTZWLWBWAYCWCKWQDFHT
      UKVPVQVRVLVS $.
      $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.
    $( A filter generates itself.  (Contributed by Jeff Hankins,
       5-Sep-2009.) $)
    fgid $p |- ( F e. Fil -> ( filGen ` F ) = F ) $=
      ( vt vx cfil wcel cfg cfv cv cuni wss wrex wa cfbas filfbas eqid elfg syl
      wb wi fillsb 3expd com34 rexlimdv com23 imp3a sylbid ssrdv fbssfg eqssd )
      ADEZAFGZAUJBUKAUJBHZUKEZULAIZJZCHZULJZCAKZLZULAEZUJAMEZUMUSRANZCULAUNUNOZ
      PQUJUOURUTUJURUOUTUJUQUOUTSCAUJUPAEZUOUQUTUJVDUOUQUTUPULAUNVCTUAUBUCUDUEU
      FUGUJVAAUKJVBAUHQUI $.
      $( [5-Sep-2009] $)
  $}

  ${
    $d u v w y z ph $.  $d u v z F $.  $d u v x y z X $.  $d u w x y z B $.
    fgfild.1 $e |- ( ph -> ( ( B e. fBas /\ U. B C_ X /\ X e. _V ) /\
                       F = { x e. ~P X | E. y e. B y C_ x } ) ) $.
    $( Lemma for ~ fgfil . $)
    fgfild $p |- ( ph -> ( F e. Fil /\ X = U. F ) ) $=
      ( vz wcel wceq cv wss wrex wa sseq2 rexbidv wsbc c0 syl wi vu vv cfil cpw
      vw cuni crab cfbas cvv w3a simprd eleq2d elrab visset anbi1i bitri syl6bb
      elpw simpld simp3d wex wne simp1d fbasne0 n0 sylib elssuni simp2d syl5com
      sstr2 ancld eximdv mpd df-rex sylibr wb sbcieg mpbird wn 0nelfb 0ex sbcie
      syl5 ss0 eleq1 biimpac rexlimiva sylbi nsyl sstr reximdv 3ad2ant3 3imtr4g
      expcom cin fbasssin 3expib ss2in com12 syl6 exp5c imp31 impancom rexlimdv
      rexlimdva imp3a 3ad2ant1 sseq1 cbvrexv anbi12i inex1 isfild isfildu jca
      ex ) AEUCIFEUFJACKZHKZLZCDMZHUAUBFEAXQEIXQXPBKZLZCDMZBFUDZUGZIZXQFLZXSNZA
      EYDXQADUHIZDUFZFLZFUIIZUJZEYDJZGUKULYEXQYCIZXSNYGYBXSBXQYCXTXQJYAXRCDXTXQ
      XPOPUMYNYFXSXQFHUNURUOUPUQZAYHYJYKAYLYMGUSZUTZAXSHFQZXPFLZCDMZAXPDIZYSNZC
      VAZYTAUUACVAZUUCADRVBZUUDAYHUUEAYHYJYKYPVCZDVDSCDVEVFAUUAUUBCAUUAYSUUAXPY
      ILZAYSXPDVGAYJUUGYSAYHYJYKYPVHXPYIFVJVIWCVKVLVMYSCDVNVOAYKYRYTVPYQXSYTHFU
      IXQFJXRYSCDXQFXPOPVQSVRZARDIZXSHRQZAYHUUIVSUUFDVTSUUJXPRLZCDMZUUIXSUULHRW
      AXQRJXRUUKCDXQRXPOPWBUUKUUICDUUKUUAUUIUUKXPRJUUAUUIVPXPWDXPRDWESWFWGWHWIA
      UAKZFLZUBKZUUMLZUJXPUUOLZCDMZXPUUMLZCDMZXSHUUOQZXSHUUMQZUUPAUURUUTTUUNUUP
      UUQUUSCDUUQUUPUUSXPUUOUUMWJWNWKWLXSUURHUUOUBUNXQUUOJXRUUQCDXQUUOXPOPWBZXS
      UUTHUUMUAUNZXQUUMJXRUUSCDXQUUMXPOPWBZWMAUUNUUOFLZUJXQUUMLZHDMZUEKZUUOLZUE
      DMZNZXPUUMUUOWOZLZCDMZUVBUVANXSHUVMQAUUNUVLUVOTUVFAUVHUVKUVOAUVGUVKUVOTZH
      DAXQDIZNZUVGUVPUVRUVGNUVJUVOUEDUVRUVIDIZUVGUVJUVOTZAUVQUVSUVGUVTTAUVQUVSU
      VGUVJUVOAUVQUVSNZXPXQUVIWOZLZCDMZUVGUVJNZUVOTAYHUWAUWDTUUFYHUVQUVSUWDCXQU
      VIDWPWQSUWEUWDUVOUWEUWBUVMLZUWDUVOTXQUUMUVIUUOWRUWFUWCUVNCDUWCUWFUVNXPUWB
      UVMVJWSWKSWSWTXAXBXCXDXOXEXFXGUVBUVHUVAUVKUVBUUTUVHUVEUUSUVGCHDXPXQUUMXHX
      IUPUVAUURUVKUVCUUQUVJCUEDXPUVIUUOXHXIUPXJXSUVOHUVMUUMUUOUVDXKXQUVMJXRUVNC
      DXQUVMXPOPWBWMXLAXSHFEYOYQUUHXMXN $.
      $( [30-Nov-2013] $)
  $}

  ${
    $d x y z F $.
    $( A generated filter is a filter.  (Contributed by Jeff Hankins,
       3-Sep-2009.)  (Revised by Mario Carneiro, 28-Nov-2013.) $)
    fgfil $p |- ( F e. fBas -> ( filGen ` F ) e. Fil ) $=
      ( vx vy vz cfbas wcel cfg cfv cfil cuni wceq wss cvv w3a cv wrex cpw crab
      id wa ssid a1i uniexg 3jca eqid elfg sseq2 elrab visset elpw anbi1i bitri
      rexbidv syl6bbr eqrdv jca fgfild simpld ) AEFZAGHZIFAJZUTJKUSBCAUTVAUSUSV
      AVALZVAMFZNUTCOZBOZLZCAPZBVAQZRZKUSUSVBVCUSSVBUSVAUAUBAEUCUDUSDUTVIUSDOZU
      TFVJVALZVDVJLZCAPZTZVJVIFZCVJAVAVAUEUFVOVJVHFZVMTVNVGVMBVJVHVEVJKVFVLCAVE
      VJVDUGUMUHVPVKVMVJVADUIUJUKULUNUOUPUQUR $.
      $( [28-Nov-2013] $) $( [3-Sep-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z F $.  $d x y X $.
    extbas.1 $e |- X = U. F $.
    $( A way to extend the base set of a filter.  (Contributed by Jeff Hankins,
       6-Sep-2009.) $)
    extbas1 $p |- ( ( F e. fBas /\ X C_ A ) -> ( F u. { A } ) e. fBas ) $=
      ( vz vx vy cfbas wcel wss wa cvv cun cv wrex wral simprl wceq syl2anr c0
      csn cin ssun1 cuni elssuni syl6sseqr adantr sstr biimpi ad2antll sseqtr4d
      df-ss sylib eqimss2 syl sseq1 rcla4ev syl11anc ssrexv mpsyl ralrimivva wb
      elsn wne id fbdmn0 ssn0 cfil snfil filfbas anasss fbun syldan mpbird expr
      wn wi snprc uneq2 sylbi un0 syl6req eleq1d biimpcd pm2.61d ) BHIZCAJZKZAL
      IZBAUAZMZHIZWFWGWIWLWFWGWIKZKZWLENZFNZGNZUBZJZEWKOZGWJPFBPZWNWTFGBWJBWKJW
      NWPBIZWQWJIZKZKZWSEBOZWTBWJUCXEXBWPWRJZXFWNXBXCQXEWRWPRZXGXEWPWQJXHXEWPAW
      QXDWPCJZWGWPAJWNXBXIXCXBWPBUDCWPBUEDUFUGWFWGWIQWPCAUHSXCWQARZWNXBXCXJGAVC
      UIUJUKWPWQULUMWPWRUNUOWSXGEWPBWOWPWRUPUQURWSEBWKUSUTVAWFWMWJHIZWLXAVBWFWG
      WIXKWIWIATVDZXKWHWIVEWGWGCTVDXLWFWGVEBCDVFCAVGSWIXLKWJVHIXKALVIWJVJUOSVKF
      GEBWJVLVMVNVOWFWIVPZWLVQWGXMWFWLXMBWKHXMWKBTMZBXMWJTRWKXNRAVRWJTBVSVTBWAW
      BWCWDUGWE $.
      $( [28-Nov-2013] $) $( [6-Sep-2009] $)

    $( The base set of an extended filter.  (Contributed by Jeff Hankins,
       7-Sep-2009.) $)
    extbas2 $p |- ( ( X C_ A /\ A e. B ) -> U. ( F u. { A } ) =
      A ) $=
      ( wss wcel csn cun cuni uniun uneq1i eqtr4i unisng uneq2d ssid jctr sylib
      wa unss ssun2 a1i eqssd sylan9eqr syl5eq ) DAFZABGZSCAHZIJZDUHJZIZAUICJZU
      JIUKCUHKDULUJELMUGUFUKDAIZAUGUJADABNOUFUMAUFUFAAFZSUMAFUFUNAPQDAATRAUMFUF
      ADUAUBUCUDUE $.
      $( [1-Dec-2010] $) $( [7-Sep-2009] $)
  $}

  ${
    $d r s u v w x y z B $.  $d r s u v w z C $.  $d r s u v w x y z F $.
    $d r s u v w x X $.
    filrn.1 $e |- X = U. B $.
    filrn.2 $e |- C = { y | E. x e. B y = ( F " x ) } $.
    $( Given a filter on a domain, produce a filter on the range.  (Contributed
       by Jeff Hankins, 7-Sep-2009.) $)
    filrn $p |- ( ( B e. fBas /\ F Fn X ) -> C e. fBas ) $=
      ( vr vu vv wcel wa c0 cv wss wrex wceq cvv wn vz vs vw cfbas wfn wne wnel
      cin wral w3a cima cmpt crn cab eqid rnmpt eqtr4i a1i wfun fnfun funimaexg
      adantl ralrimiva dmmpt 3syl fbasne0 adantr eqnetrd dm0rn0 necon3bii sylib
      cdm cuni wi elssuni 0nelfb eleq1 notbid syl5ibrcom con2d jcad fndm syl6eq
      sseq2d biimpar sseqin2 eqeq1d biimpd con3d expimpd imadisj notbii syl6ibr
      eqcom bitri syld ralrimiv eleq2i eqeq1 rexbidv elab bitr2i ralnex 3bitr4i
      df-nel visset imaeq2 eqeq2d cbvrexv 3bitri anbi12i reeanv bitr4i fbasssin
      0ex 3expb ad2ant2r rcla4ev mpan2 ad2antrl wb funimaex elabg syl5bb mpbird
      ad2antrr imass2 ad2antll inss1 ax-mp inss2 ssini ad2antlr syl5sseqr sstrd
      ineq12 sseq1 syl11anc adantlrl exp32 rexlimdv mpd rexlimdvv 3jca abrexexg
      syl5bi ralrimivv syl5eqel isfbas2 syl ) CUDLZEFUEZMZDUDLZDNUFZNDUGZUAOZIO
      ZUBOZUHZPZUADQZUBDUIIDUIZUJZUUMUUOUUPUVCUUMDACEAOZUKZULZUMZNDUVHRUUMDBOZU
      VFRZACQZBUNZUVHHABCUVFUVGUVGUOUPUQURUUMUVGVLZNUFUVHNUFUUMUVMCNUUMEUSZUVFS
      LZACUIUVMCRUULUVNUUKFEUTZVBUVNUVOACEUVECVAVCACUVFSVDVEUUKCNUFUULCVFVGVHUV
      MNUVHNUVGVIVJVKVHUUMNUVFRZTZACUIZUUPUUMUVRACUUMUVECLZUVECVMZPZUVENRZTZMZU
      VRUUMUVTUWBUWDUVTUWBVNUUMUVECVOURUUKUVTUWDVNUULUUKUWCUVTUUKUVTTUWCNCLZTCV
      PUWCUVTUWFUVENCVQVRVSVTVGWAUUMUWEEVLZUVEUHZNRZTZUVRUUMUWBUWDUWJUUMUWBMZUW
      IUWCUWKUWIUWCUWKUWHUVENUWKUVEUWGPZUWHUVERUUMUWLUWBUUMUWGUWAUVEUUMUWGFUWAU
      ULUWGFRUUKFEWBVBGWCWDWEUVEUWGWFVKWGWHWIWJUVQUWIUVQUVFNRUWINUVFWNEUVEWKWOW
      LWMWPWQUVQACQZTNDLZTUVSUUPUWMUWNUWNNUVLLUWMDUVLNHWRUVKUWMBNXOUVINRUVJUVQA
      CUVINUVFWSWTXAXBWLUVQACXCNDXEXDVKUUMUVBIUBDDUURDLZUUSDLZMZUUREJOZUKZRZUUS
      EKOZUKZRZMZKCQJCQZUUMUVBUWQUWTJCQZUXCKCQZMUXEUWOUXFUWPUXGUWOUURUVLLUURUVF
      RZACQZUXFDUVLUURHWRUVKUXIBUURIXFUVIUURRUVJUXHACUVIUURUVFWSWTXAUXHUWTAJCUV
      EUWRRUVFUWSUURUVEUWREXGXHXIXJUWPUUSUVLLUUSUVFRZACQZUXGDUVLUUSHWRUVKUXKBUU
      SUBXFUVIUUSRUVJUXJACUVIUUSUVFWSWTXAUXJUXCAKCUVEUXARUVFUXBUUSUVEUXAEXGXHXI
      XJXKUWTUXCJKCCXLXMUUMUXDUVBJKCCUUMUWRCLZUXACLZMZUXDUVBUUMUXNUXDMMZUCOZUWR
      UXAUHZPZUCCQZUVBUUKUXNUXSUULUXDUUKUXLUXMUXSUCUWRUXACXNXPXQUXOUXRUVBUCCUXO
      UXPCLZUXRUVBUUMUXDUXTUXRMZUVBUXNUUMUXDMZUYAMZEUXPUKZDLZUYDUUTPZUVBUYCUYEU
      YDUVFRZACQZUXTUYHUYBUXRUXTUYDUYDRZUYHUYDUOUYGUYIAUXPCUVEUXPRUVFUYDUYDUVEU
      XPEXGXHXRXSXTUUMUYEUYHYAZUXDUYAUULUYJUUKUYEUYDUVLLZUULUYHDUVLUYDHWRUULUVN
      UYDSLUYKUYHYAUVPEUXPUCXFYBUVKUYHBUYDSUVIUYDRUVJUYGACUVIUYDUVFWSWTYCVEYDVB
      YFYEUYCUYDEUXQUKZUUTUXRUYDUYLPUYBUXTUXPUXQEYGYHUYCUWSUXBUHZUYLUUTUYLUWSUX
      BUXQUWRPUYLUWSPUWRUXAYIUXQUWREYGYJUXQUXAPUYLUXBPUWRUXAYKUXQUXAEYGYJYLUXDU
      UTUYMRUUMUYAUURUWSUUSUXBYPYMYNYOUVAUYFUAUYDDUUQUYDUUTYQXRYRYSYTUUAUUBYTUU
      CUUFUUGUUDUUKUUNUVDYAZUULUUKDSLUYNUUKDUVLSHABCUVFUDUUEUUHIUBUASDUUIUUJVGY
      E $.
      $( [28-Nov-2013] $) $( [7-Sep-2009] $)
  $}

  ${
    $d f g h x y F $.  $d f g x y X $.
    $( The union of a nonempty set of filters with a common base and closed
       under pairwise union is a filter.  (Contributed by Mario Carneiro,
       28-Nov-2013.) $)
    filuni $p |- ( ( F C_ Fil /\ F =/= (/) /\ A. f e. F ( U. f = X /\
           A. g e. F ( f u. g ) e. F ) ) -> ( U. F e. Fil /\ X = U. U. F ) ) $=
      ( vx vh cfil wss c0 cv wcel wa w3a wrex eluni2 wi syl ex cvv wsbc vy cuni
      wne wceq wral r19.29 simpl elssuni sseq2 biimpa syl2an rexlimivw 3ad2ant3
      syl5bi pm4.71rd r19.2z visset uniex eleq1 mpbii adantr 3adant1 ssel2 eqid
      cun filusb syl5ibcom ralimdva sylan9 3impia sylibr sbcel1gv mpbird filesn
      wb ralrimiva 3ad2ant1 0ex ax-mp bitri notbii ralnex bitr4i simp13 simprrr
      simp1 simpl2 simprll adantl sseqtr4d simpl3 fillsb syl13anc expr reximdva
      imp syl3an1 syld sbid clelsb3 3imtr4g cin anim1d an32 simplrr elun1 elun2
      anim12i eleq2 anbi12d rcla4ev sylan2 an12s rexlimdva syl9r impr rexlimiva
      wn sylan2b simp11 filint 3expib syl5 anbi12i inex1 isfild isfildu jca ) C
      GHZCIUCZAJZUBZDUDZYKBJZVEZCKZBCUEZLZACUEZMZCUBZGKDUUAUBUDYTEJZUUAKZEUAEDU
      UAYTUUCUUBDHZUUCUUBYKKZACNZYTUUDAUUBCOZYSYIUUFUUDPYJYSUUFUUDYSUUFLYRUUELZ
      ACNZUUDYRUUEACUFZUUHUUDACYRYMUUBYLHZUUDUUEYMYQUGUUBYKUHYMUUKUUDYLDUUBUIUJ
      UKULQRUMUNUOZYJYSDSKZYIYJYSLYRACNUUMYRACUPYRUUMACYMUUMYQYMYLSKUUMYKAUQURY
      LDSUSUTVAULQVBZYTUUCEDTZDUUAKZYTDYKKZACNZUUPYIYJYSUURYIYSUUQACUEZYJUURYIY
      RUUQACYIYKCKZLZYLYKKZYRUUQUVAYKGKZUVBCGYKVCZYKYLYLVDZVFQYMUVBUUQVOYQYLDYK
      USVAVGVHYJUUSUURUUQACUPRVIVJADCOVKYTUUMUUOUUPVOUUNEDUUASVLQVMZYTIYKKZXRZA
      CUEZUUCEITZXRZYIYJUVIYSYIUVHACUVAUVCUVHUVDYKVNQVPVQUVKUVGACNZXRUVIUVJUVLU
      VJIUUAKZUVLISKUVJUVMVOVREIUUASVLVSAICOVTWAUVGACWBWCVKYTUAJZDHZUUBUVNHZMZU
      UFUVNYKKZACNZUUCEUUBTZUUCEUVNTZUVQUUFUUIUVSUVQYSUUFUUIPYIYJYSUVOUVPWDYSUU
      FUUIUUJRQYTYIUVOUVPUUIUVSPYIYJYSWFYIUVOUVPMZUUHUVRACUWBUUTUUHUVRUWBUUTUUH
      LZLZUVCUUEUVNYLHZUVPUVRUWBYIUUTUVCUWCYIUVOUVPWFUUTUUHUGUVDUKUWBUUTYRUUEWE
      UWDUVNDYLYIUVOUVPUWCWGUWCYMUWBUUTYMYQUUEWHWIWJYIUVOUVPUWCWKUVCUUEUWEUVPMU
      VRUUBUVNYKYLUVEWLWPWMWNWOWQWRUVTUUCUUFUUCEWSZUUGVTUWAUVNUUAKUVSUAEUUAWTAU
      VNCOVTZXAYTUVOUUDMZUVSUUBYNKZBCNZLZUVNUUBXBZFJZKZFCNZUWAUVTLUUCEUWLTZUWHU
      WKYRUVRLZACNZUWJLZUWOUWHUVSUWRUWJUWHYSUVSUWRPYIYJYSUVOUUDWDYSUVSUWRYRUVRA
      CUFRQXCUWSUVNUWMKZUUBUWMKZLZFCNZUWHUWOUWRUWJUXCUWQUWJUXCPZACUWQUUTYMUVRLZ
      YQLUXDYMYQUVRXDUUTUXEYQUXDYQUWJYPUWILZBCNZUUTUXELZUXCYQUWJUXGYPUWIBCUFRUX
      HUXFUXCBCUXHYNCKZLUVRUXFUXCPUUTYMUVRUXIXEUVRUXFUXCYPUVRUWIUXCUVRUWILYPUVN
      YOKZUUBYOKZLZUXCUVRUXJUWIUXKUVNYKYNXFUUBYNYKXGXHUXBUXLFYOCUWMYOUDUWTUXJUX
      AUXKUWMYOUVNXIUWMYOUUBXIXJXKXLXMRQXNXOXPXSXQWPUWHYIUXCUWOPYIYJYSUVOUUDXTY
      IUXBUWNFCYIUWMCKLUWMGKZUXBUWNPCGUWMVCUXMUWTUXAUWNUVNUUBUWMYAYBQWOQYCWRUWA
      UVSUVTUWJUWGUVTUUCUWJUWFBUUBCOVTYDUWPUWLUUAKZUWOUWLSKUWPUXNVOUVNUUBUAUQYE
      EUWLUUASVLVSFUWLCOVTXAYFYTUUCEDUUAUULUUNUVFYGYH $.
      $( [28-Nov-2013] $)
  $}


