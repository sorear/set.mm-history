$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Filters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Fil $. $( The class of all filters. $)

  $( Extend class notation with the class of all filters. $)
  cfil $a class Fil $.

  ${
    $d f x y $.
    $( The class of all filters.  Bourbaki TG I.36 def. 1 axioms FI, FIIa,
       FIIb, FIII. Filters are used to define the concept of limit in the
       general case.  It's a generalization of the idea of neighborhoods.
       Suppose you are in ` RR ` .  With neighborhoods you can express the idea
       of a variable that tends to a specific number but you can't express the
       idea of a variable that tends to infinity.  Filters relax the "axioms"
       of neighborhoods and then succeed in expressing the idea of something
       that tends to infinity.  Filters were invented by Cartan in 1937 and
       made famous by Bourbaki in his treatise.  A notion similar to the notion
       of filter is the concept of net invented by Moore and Smith in 1922.
       (Contributed by FL, 20-Jul-2007.) $)
    df-fil $a |- Fil = { f e. fBas |
      A. x e. ~P U. f ( ( f i^i ~P x ) =/= (/) -> x e. f ) } $.
  $}

  ${
    $d F f x $.  $d X f x $.
    isfil.1 $e |- X = U. F $.
    $( The predicate "is a filter."  (Contributed by FL, 20-Jul-2007.) $)
    isfil $p |- ( F e. Fil <-> ( F e. fBas /\
      A. x e. ~P X ( ( F i^i ~P x ) =/= (/) -> x e. F ) ) ) $=
      ( vf cv cpw cin c0 wne wcel wi cuni wral cfbas cfil wceq unieq syl6eqr
      pweq syl ineq1 neeq1d eleq2 imbi12d raleqbidv df-fil elrab2 ) EFZAFZGZHZI
      JZUJUIKZLZAUIMZGZNBUKHZIJZUJBKZLZACGZNEBOPUIBQZUOVAAUQVBVCUPCQUQVBQVCUPBM
      CUIBRDSUPCTUAVCUMUSUNUTVCULURIUIBUKUBUCUIBUJUDUEUFAEUGUH $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d x F $.
    $( A filter is a filter base.  (Contributed by Jeff Hankins,
       2-Sep-2009.) $)
    filfbas $p |- ( F e. Fil -> F e. fBas ) $=
      ( vx cfil wcel cfbas cv cpw cin c0 wne wi cuni wral eqid isfil simplbi )
      ACDAEDABFZGHIJQADKBALZGMBARRNOP $.
      $( [27-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d F x y $.
    $( The empty set doesn't belong to a filter.  (Contributed by FL,
       20-Jul-2007.) $)
    filesn $p |- ( F e. Fil -> -. (/) e. F ) $=
      ( cfil wcel cfbas c0 wn filfbas 0nelfb syl ) ABCADCEACFAGAHI $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d B x $.  $d F x $.  $d X x $.
    fillsb.1 $e |- X = U. F $.
    $( A filter is closed under taking supersets.  (Contributed by FL,
       20-Jul-2007.) $)
    fillsb $p |- ( F e. Fil ->
                   ( ( A e. F /\ B C_ X /\ A C_ B ) -> B e. F ) ) $=
      ( vx cfil wcel wss w3a cpw cin c0 wne wa cvv cuni uniexg syl5eqel simp2
      elpw2g biimpar syl2an simp1 elpwg 3adant2 elin sylanbrc ne0i syl adantl
      jca cv wi wral cfbas isfil simprbi wceq pweq ineq2d neeq1d eleq1 imbi12d
      rcla4cv imp32 sylan syldan ex ) CGHZACHZBDIZABIZJZBCHZVJVNBDKZHZCBKZLZMNZ
      OZVOVJVNOVQVTVJDPHZVLVQVNVJDCQPECGRSVKVLVMTWBVQVLBDPUAUBUCVNVTVJVNAVSHZVT
      VNVKAVRHZWCVKVLVMUDVKVMWDVLVKWDVMABCUEUBUFACVRUGUHVSAUIUJUKULVJCFUMZKZLZM
      NZWECHZUNZFVPUOZWAVOVJCUPHWKFCDEUQURWKVQVTVOWJVTVOUNFBVPWEBUSZWHVTWIVOWLW
      GVSMWLWFVRCWEBUTVAVBWEBCVCVDVEVFVGVHVI $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.
    $( A filter is closed under taking intersections.  (Contributed by FL,
       20-Jul-2007.) $)
    filint $p |- ( ( F e. Fil /\ A e. F /\ B e. F ) -> ( A i^i B ) e. F ) $=
      ( vx cfil wcel w3a cv cin wss wrex cfbas filfbas fbasssin syl3an1 wi
      cuni inss1 a1i elssuni sstrd wa eqid fillsb 3expd com23 imp r19.23adv
      sylan2 3adant3 mpd ) CEFZACFZBCFZGDHZABIZJZDCKZUPCFZULCLFUMUNURCMDABCNOUL
      UMURUSPZUNUMULUPCQZJZUTUMUPAVAUPAJUMABRSACTUAULVBUBUQUSDCULVBUOCFZUQUSPZP
      ULVCVBVDULVCVBUQUSUOUPCVAVAUCUDUEUFUGUHUIUJUK $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d F x $.  $d X x $.
    filusb.1 $e |- X = U. F $.
    $( The underlying set belongs to the filter.  (Contributed by FL,
       20-Jul-2007.) $)
    filusb $p |- ( F e. Fil -> X e. F ) $=
      ( vx cfil wcel c0 wne cfbas filfbas fbasne0 syl cv wex n0 wa wss cuni
      elssuni syl6sseqr adantl wi ssid fillsb 3expd imp mpi mpd ex 19.23adv
      syl5bi ) AEFZAGHZBAFZULAIFUMAJAKLUMDMZAFZDNULUNDAOULUPUNDULUPUNULUPPZUOBQ
      ZUNUPURULUPUOARBUOASCTUAUQBBQZURUNUBZBUCULUPUSUTUBULUPUSURUNUOBABCUDUEUFU
      GUHUIUJUKUH $.
      $( [27-Nov-2013] $) $( [20-Jul-2007] $)
  $}

  ${
    $d F x y z $.  $d X x y $.
    isfil2.1 $e |- X = U. F $.
    $( Derive the standard axioms of a filter.  (Contributed by Mario Carneiro,
       27-Nov-2013.) $)
    isfil2 $p |- ( F e. Fil <-> ( ( -. (/) e. F /\ X e. F ) /\
        A. x e. ~P X ( E. y e. F y C_ x -> x e. F ) /\
        A. x e. F A. y e. F ( x i^i y ) e. F ) ) $=
      ( vz cfil wcel c0 wn wa cv wss wrex wi cpw wral cin w3a filesn filusb
      jca elpwi fillsb 3expd com23 imp r19.23adv sylan2 r19.21aiva filint
      3expb r19.21aivva 3jca cfbas wne cvv wnel simp1r cuni pwuni eleq1i pwexg
      sylbi ssexg sylancr syl ne0i simp1l df-nel sylibr ssid sseq1 rcla4ev
      mpan2 ralimi 3ad2ant3 isfbas2 biimpar syl13anc wex elin anim2i eximi n0
      df-rex 3imtr4i imim1i 3ad2ant2 isfil sylanbrc impbii ) CGHZICHJZDCHZKZBLZ
      ALZMZBCNZWRCHZOZADPZQZWRWQRZCHZBCQZACQZSZWMWPXDXHWMWNWOCTCDEUAUBWMXBAXCWR
      XCHWMWRDMZXBWRDUCWMXJKWSXABCWMXJWQCHZWSXAOZOWMXKXJXLWMXKXJWSXAWQWRCDEUDUE
      UFUGUHUIUJWMXFABCCWMXAXKXFWRWQCUKULUMUNXICUOHZCWRPZRZIUPZXAOZAXCQZWMXICUQ
      HZCIUPZICURZFLZXEMZFCNZBCQZACQZXMXIWOXSWNWOXDXHUSZWOCCUTZPZMYIUQHZXSCVAWO
      YHCHYJDYHCEVBYHCVCVDCYIUQVEVFVGXIWOXTYGCDVHVGXIWNYAWNWOXDXHVIICVJVKXHWPYF
      XDXGYEACXFYDBCXFXEXEMZYDXEVLYCYKFXECYBXEXEVMVNVOVPVPVQXSXMXTYAYFSABFUQCVR
      VSVTXDWPXRXHXBXQAXCXPWTXAWQXOHZBWAXKWSKZBWAXPWTYLYMBYLXKWQXNHZKYMWQCXNWBY
      NWSXKWQWRUCWCVDWDBXOWEWSBCWFWGWHVPWIACDEWJWKWL $.
      $( [27-Nov-2013] $)
  $}

  ${
    $d x y A $.  $d z A $.  $d x y F $.  $d y z F $.  $d x y ph $.
    $d y z ph $.  $d y ps $.  $d y B $.
    isfild.1 $e |- ( ph -> ( x e. F <-> ( x C_ A /\ ps ) ) ) $.
    isfild.2 $e |- ( ph -> A e. _V ) $.
    $( Lemma for ~ isfild . $)
    isfildlem $p |- ( ph -> ( B e. F <-> ( B C_ A /\ [ B / x ] ps ) ) ) $=
      ( vy cvv wcel wss wsbc wa wi elisset a1i ssexg expcom syl adantrd wb cv
      wceq eleq1 sseq1 dfsbcq anbi12d bibi12d imbi2d ax-17 hbs1 hban hbbi hbim
      visset sbceq1a vtoclf vtoclg com12 pm5.21ndd ) AEJKZEFKZEDLZBCEMZNZVCVBOA
      EFPQAVDVBVEADJKZVDVBOHVDVGVBEDJRSTUAVBAVCVFUBZAIUCZFKZVIDLZBCVIMZNZUBZOZA
      VHOIEJVIEUDZVNVHAVPVJVCVMVFVIEFUEVPVKVDVLVEVIEDUFBCVIEUGUHUIUJACUCZFKZVQD
      LZBNZUBZOVOCVIAVNCACUKVJVMCVJCUKVKVLCVKCUKBCIULUMUNUOIUPVQVIUDZWAVNAWBVRV
      JVTVMVQVIFUEWBVSVKBVLVQVIDUFBCVIUQUHUIUJGURUSUTVA $.
      $( [1-Dec-2013] $)

    isfild.3 $e |- ( ph -> [ A / x ] ps ) $.
    $( Base set of a filter of the form ` { x e. ~P A | ph } ` .  (Contributed
       by Mario Carneiro, 14-Dec-2013.) $)
    isfildu $p |- ( ph -> A = U. F ) $=
      ( cuni wcel wss wsbc wa ssid jctil isfildlem mpbird elssuni syl cv wral
      simpl syl6bi r19.21aiv unissb sylibr eqssd ) ADEIZADEJZDUHKAUIDDKZBCDLZMA
      UKUJHDNOABCDDEFGPQDERSACTZDKZCEUAUHDKAUMCEAULEJUMBMUMFUMBUBUCUDCEDUEUFUG
      $.
      $( [14-Dec-2013] $)

    isfild.4 $e |- ( ph -> -. [ (/) / x ] ps ) $.
    isfild.5 $e |- ( ( ph /\ y C_ A /\ z C_ y ) ->
                     ( [ z / x ] ps -> [ y / x ] ps ) ) $.
    isfild.6 $e |- ( ( ph /\ y C_ A /\ z C_ A ) ->
        ( ( [ y / x ] ps /\ [ z / x ] ps ) -> [ ( y i^i z ) / x ] ps ) ) $.
    $( Sufficient condition for a set of the form ` { x e. ~P A | ph } ` to be
       a filter.  (Contributed by Mario Carneiro, 1-Dec-2013.) $)
    isfild $p |- ( ph -> F e. Fil ) $=
      ( c0 wcel wn cuni wa cv wss wrex wi cpw wral cin w3a cfil wsbc isfildlem
      simpr syl6bi mtod visset elpw biimpri adantr ssrdv sspwuni sylib ssid
      jctil mpbird elssuni syl eqssd eqeltrd jca elpwi sseq2d simp2 a1d jcad
      adantld wb 3ad2ant1 3imtr4d 3expa impancom r19.23adva ex sylbid syl5
      r19.21aiv ssinss1 ad2antrr a1i an4 3expb expimpd syl5bi anbi12d
      r19.21aivv 3jca eqid isfil2 sylibr ) ANGOZPZGQZGOZRZESZDSZTZEGUAXCGOZUBZD
      WSUCZUDZXCXBUEZGOZEGUDDGUDZUFGUGOAXAXHXKAWRWTAWQBCNUHZKAWQNFTZXLRXLABCFNG
      HIUIXMXLUJUKULAWSFGAWSFAGFUCZTWSFTACGXNACSZGOXOFTZBRXOXNOZHXPXQBXQXPXOFCU
      MUNUOUPUKUQGFURUSAFGOZFWSTAXRFFTZBCFUHZRAXTXSJFUTVAABCFFGHIUIVBZFGVCVDVEZ
      YAVFVGAXFDXGXCXGOXCWSTZAXFXCWSVHAYCXCFTZXFAWSFXCYBVIAYDXFAYDRZXDXEEGYEXDX
      BGOZXEAYDXDYFXEUBAYDXDUFZXBFTZBCXBUHZRZYDBCXCUHZRZYFXEYGYIYLYHYGYIYDYKYGY
      DYIAYDXDVJVKLVLVMAYDYFYJVNXDABCFXBGHIUIZVOAYDXEYLVNXDABCFXCGHIUIZVOVPVQVR
      VSVTWAWBWCAXJDEGGAYLYJRZXIFTZBCXIUHZRXEYFRXJAYOYPYQYOYPUBAYDYPYKYJXCXBFWD
      WEWFYOYDYHRZYKYIRZRAYQYDYKYHYIWGAYRYSYQAYDYHYSYQUBMWHWIWJVLAXEYLYFYJYNYMW
      KABCFXIGHIUIVPWLWMDEGWSWSWNWOWP $.
      $( [1-Dec-2013] $)
  $}

  ${
    $d F x y $.
    $( A filter is closed under taking intersections.  (Contributed by Mario
       Carneiro, 27-Nov-2013.) $)
    filfi $p |- ( F e. Fil -> ( fi ` F ) = F ) $=
      ( vx vy cfil wcel cv cin wral cfi cfv wceq filint 3expib r19.21aivv
      inficl mpbid ) ADEZBFZCFZGAEZCAHBAHAIJAKQTBCAAQRAESAETRSALMNBCADOP $.
      $( [27-Nov-2013] $)
  $}

  $( The intersection of two elements of a filter can't be empty.  (Contributed
     by FL, 19-Sep-2007.) $)
  fipfil $p |- ( F e. Fil -> ( ( A e. F /\ B e. F ) ->
    ( A i^i B ) =/= (/) ) ) $=
    ( cfil wcel wa cin c0 wceq wn wne filint 3expb filesn adantr nelneq
    syl11anc ex df-ne syl6ibr ) CDEZACEZBCEZFZABGZHIJZUEHKUAUDUFUAUDFUECEZHCEJZ
    UFUAUBUCUGABCLMUAUHUDCNOUEHCPQRUEHST $.
    $( [1-Dec-2010] $) $( [16-Sep-2007] $)

  $( A filter has the finite intersection property.  Bourbaki TG I.36 note of
     def. 1.  (Contributed by FL, 2-Sep-2007.) $)
  fipfil2 $p |- ( F e. Fil -> ( ( A C_ F /\ A =/= (/) /\ A e. Fin ) ->
                  |^| A =/= (/) ) ) $=
    ( cfil wcel wss c0 wne cfn w3a cint cfi cfv elfir ex filfi eleq2d sylibd
    wn wceq filesn eleq1 notbid syl5ibrcom necon2ad syld ) BCDZABEAFGAHDIZAJZBD
    ZUHFGUFUGUHBKLZDZUIUFUGUKABCMNUFUJBUHBOPQUFUIUHFUFUIRUHFSZFBDZRBTULUIUMUHFB
    UAUBUCUDUE $.
    $( [27-Nov-2013] $) $( [20-Sep-2007] $)

  $( The empty set is not a filter.  Bourbaki TG I.36 def 1 note.  (Contributed
     by FL, 31-Oct-2007.) $)
  emnfil $p |- ( F e. Fil -> F =/= (/) ) $=
    ( cfil wcel cuni c0 wne eqid filusb ne0i syl ) ABCADZACAEFAKKGHAKIJ $.
    $( [27-Nov-2013] $) $( [30-Oct-2007] $)

  ${
    $d F x y $.  $d G x y $.
    $( The intersection of two filters is a filter.  Use ~ fiint to extend this
       property to the intersection of a finite set of filters.  Bourbaki TG
       I.36 par. 3.  (Contributed by FL, 19-Sep-2007.) $)
    infil $p |- ( ( F e. Fil /\ G e. Fil /\ ( F i^i G ) =/= (/) ) ->
      ( F i^i G ) e. Fil ) $=
      ( vy vx cfil wcel cin c0 wne w3a wn cuni wa cv wss wrex wi cpw wral
      filesn 3ad2ant1 inss1 sseli nsyl cvv ssexg mpan uniexg syl ssid elpwg
      mpbiri simpl1 simpr2 simpr1 elpwi uniss ax-mp sstr mpan2 3syl simpr3
      eqid fillsb imp syl13anc simpl2 inss2 elin sylanbrc ex 3expd r19.23adv
      r19.21aiva wex elssuni ancli eximi n0 df-rex 3imtr4i 3ad2ant3 wceq sseq2
      rexbidv eleq1 imbi12d rcla4v syl3c jca simpl anim12i filint 3expb syl2an
      simpr r19.21aivva 3adant3 3jca isfil2 sylibr ) AEFZBEFZABGZHIZJZHXDFZKZXD
      LZXDFZMZCNZDNZOZCXDPZXMXDFZQZDXIRZSZXMXLGZXDFZCXDSDXDSZJXDEFXFXKXSYBXFXHX
      JXFHAFZXGXBXCYCKXEATUAXDAHABUBZUCUDXFXIXRFZXSXLXIOZCXDPZXJXBXCYEXEXBXIUEF
      ZYEXBXDUEFZYHXDAOZXBYIYDXDAEUFUGXDUEUHUIYHYEXIXIOXIUJXIXIUEUKULUIUAXFXQDX
      RXFXMXRFZMXNXPCXDXFYKXLXDFZXNXPQQXFYKYLXNXPXFYKYLXNJZXPXFYMMZXMAFZXMBFZXP
      YNXBXLAFZXMALZOZXNYOXBXCXEYMUMYNYLYQXFYKYLXNUNZXDAXLYDUCZUIYNYKXMXIOZYSXF
      YKYLXNUOZXMXIUPZUUBXIYROZYSYJUUEYDXDAUQURXMXIYRUSUTVAXFYKYLXNVBZXBYQYSXNJ
      YOXLXMAYRYRVCVDVEVFYNXCXLBFZXMBLZOZXNYPXBXCXEYMVGYNYLUUGYTXDBXLABVHZUCZUI
      YNYKUUBUUIUUCUUDUUBXIUUHOZUUIXDBOUULUUJXDBUQURXMXIUUHUSUTVAUUFXCUUGUUIXNJ
      YPXLXMBUUHUUHVCVDVEVFXMABVIVJVKVLVEVMVNZXEXBYGXCYLCVOYLYFMZCVOXEYGYLUUNCY
      LYFXLXDVPVQVRCXDVSYFCXDVTWAWBXQYGXJQDXIXRXMXIWCZXOYGXPXJUUOXNYFCXDXMXIXLW
      DWEXMXIXDWFWGWHWIWJUUMXBXCYBXEXBXCMZYADCXDXDUUPXPYLMZMXTAFZXTBFZYAUUPXBYO
      YQMUURUUQXBXCWKXPYOYLYQXDAXMYDUCUUAWLXBYOYQUURXMXLAWMWNWOUUPXCYPUUGMUUSUU
      QXBXCWPXPYPYLUUGXDBXMUUJUCUUKWLXCYPUUGUUSXMXLBWMWNWOXTABVIVJWQWRWSDCXDXIX
      IVCWTXA $.
      $( [27-Nov-2013] $) $( [17-Sep-2007] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( A singleton is a filter.  Bourbaki TG I.36, example 1.  (Contributed by
       FL, 19-Sep-2007.) $)
    snfil $p |- ( ( A e. B /\ A =/= (/) ) -> { A } e. Fil ) $=
      ( vx vy wcel c0 wne wa cv wceq csn wss wb elsn eqimss pm4.71ri bitri a1i
      cvv elisset adantr wsbc eqid eqsbc3 mpbiri wn simpr necomd df-ne sylib
      0ex ax-mp mtbird w3a wi sseq1 anbi2d eqss biimpri syl6bi com12 3adant1
      visset 3imtr4g cin ineq12 inidm syl6eq anbi12i inex1 3imtr4i isfild ) ABE
      ZAFGZHZCIZAJZCDCAAKZVPVREZVPALZVQHZMVOVSVQWACANVQVTVPAOPQRVMASEVNABTUAVMV
      QCAUBZVNVMWBAAJAUCCAABUDUEUAVOVQCFUBZFAJZVOFAGWDUFVOAFVMVNUGUHFAUIUJWCWDM
      ZVOFSEWEUKCFASUDULRUMVODIZALZVPWFLZUNVQWFAJZVQCVPUBZVQCWFUBZWGWHVQWIUOVOV
      QWGWHHZWIVQWLWGAWFLZHZWIVQWHWMWGVPAWFUPUQWIWNWFAURUSUTVAVBVPSEWJVQMCVCCVP
      ASUDULZWFSEWKWIMDVCZCWFASUDULZVDWKWJHZVQCWFVPVEZUBZUOVOWGVTUNWIVQHZWSAJZW
      RWTXAWSAAVEAWFAVPAVFAVGVHWKWIWJVQWQWOVIWSSEWTXBMWFVPWPVJCWSASUDULVKRVL $.
      $( [1-Dec-2013] $) $( [16-Sep-2007] $)
  $}

  ${
    $d x y z A $.  $d x y V $.
    $( A condition for a set to generate a filter base.  (Contributed by Jeff
       Hankins, 2-Sep-2009.) $)
    fsubbas $p |- ( A e. V -> ( ( fi ` A ) e. fBas <->
                    ( A =/= (/) /\ -. (/) e. ( fi ` A ) ) ) ) $=
      ( vz vx vy wcel cfi cfv cfbas c0 wne wn wa fbasne0 fi0 necon3bid biimpar
      sylan2 0nelfb adantl jca ex w3a wnel cv cin wss wrex wral biimpa 3adant3
      simp3 df-nel sylibr fiin ssid sseq1 rcla4ev sylancl rgen2 a1i 3jca cvv
      wb fvex isfbas2 ax-mp 3expib impbid ) ABFZAGHZIFZAJKZJVKFLZMZVJVLVOVJVLMV
      MVNVLVJVKJKZVMVKNVJVMVPVJAJVKJABOPZQRVLVNVJVKSTUAUBVJVMVNVLVJVMVNUCZVPJVK
      UDZCUEZDUEZEUEZUFZUGZCVKUHZEVKUIDVKUIZUCZVLVRVPVSWFVJVMVPVNVJVMVPVQUJUKVR
      VNVSVJVMVNULJVKUMUNWFVRWEDEVKVKWAVKFWBVKFMWCVKFWCWCUGZWEWAWBAUOWCUPWDWHCW
      CVKVTWCWCUQURUSUTVAVBVKVCFVLWGVDAGVEDECVCVKVFVGUNVHVI $.
      $( [25-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x y z F $.
    $( A filter base has the finite intersection property.  (Contributed by
       Jeff Hankins, 2-Sep-2009.) $)
    fbasfip $p |- ( F e. fBas -> -. (/) e. ( fi ` F ) ) $=
      ( vy vz cfbas wcel c0 cfi cfv cv cint wceq cpw cfn cin wrex wa wss wn
      elin elpwi anim1i sylbi fbssint 3expb sylan2 0nelfb ad2antrr wi eleq1
      biimpcd adantl mtod ss0 nsyl adantrr sseq2 biimprcd ad2antll exp32
      r19.23adv mpd nrexdv cvv wb 0ex elfi mpan mtbird ) ADEZFAGHEZFBIZJZKZBALZ
      MNZOZVIVMBVOVIVKVOEZPZCIZVLQZCAOZVMRZVQVIVKAQZVKMEZPZWAVQVKVNEZWDPWEVKVNM
      SWFWCWDVKATUAUBVIWCWDWACVKAUCUDUEVRVTWBCAVRVSAEZVTWBVRWGVTPPVMVSFQZVRWGWH
      RVTVRWGPZVSFKZWHWIWJFAEZVIWKRVQWGAUFUGWGWJWKUHVRWJWGWKVSFAUIUJUKULVSUMUNU
      OVTVMWHUHVRWGVMWHVTFVLVSUPUQURULUSUTVAVBFVCEVIVJVPVDVEBFAVCDVFVGVH $.
      $( [25-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d w x y z G $.  $d x y z F $.
    $( A helpful lemma for showing that certain sets generate filters.
       (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fbunfip $p |- ( ( F e. fBas /\ G e. fBas ) ->
                    ( -. (/) e. ( fi ` ( F u. G ) ) <->
                      A. x e. F A. y e. G ( x i^i y ) =/= (/) ) ) $=
      ( vz vw cfbas wcel wa c0 cun cfi cfv wn cv cin wceq wrex wne wral w3o
      elfiun notbid w3a 3ioran df-3an bitr2i syl6bbr wb necom df-ne bitri
      ralbii ralnex a1i fbasfip anim12i biantrurd bitr2d wss wi ssfii ssralv
      syl ralimdv sylan9 ineq1 neeq1d ineq2 cbvral2v fbssfi r19.29 ss2in sseq2
      ss0 syl6bi syl5com necon3d ex com13 imp r19.23aivw impancom expimpd
      com12 syl2an an4s an32s r19.21aivva syl5bi impbid 3bitrd ) CGHZDGHZIZJCDK
      LMHZNZJCLMZHZNZJDLMZHZNZIZJAOZBOZPZQZBXARZAWRRZNZIZXGJSZBXATZAWRTZXMBDTZA
      CTZWOWQWSXBXJUAZNZXLWOWPXRABJCDGGUBUCXSWTXCXKUDXLWSXBXJUEWTXCXKUFUGUHWOXO
      XKXLXOXKUIWOXOXINZAWRTXKXNXTAWRXNXHNZBXATXTXMYABXAXMJXGSYAXGJUJJXGUKULUMX
      HBXAUNULUMXIAWRUNULUOWOXDXKWMWTWNXCCUPDUPUQURUSWOXOXQWMXOXNACTZWNXQWMCWRU
      TXOYBVACGVBXNACWRVCVDWNXNXPACWNDXAUTXNXPVADGVBXMBDXAVCVDVEVFXQEOZFOZPZJSZ
      FDTZECTZWOXOXMYFYCXFPZJSABEFCDXEYCQXGYIJXEYCXFVGVHXFYDQYIYEJXFYDYCVIVHVJW
      OYHXOWOYHIXMABWRXAWOXEWRHZXFXAHZIZYHXMWOYLIYHXMWMYJWNYKYHXMVAZWMYJIYCXEUT
      ZECRZYDXFUTZFDRZYMWNYKIEXECVKFXFDVKYHYOYQIXMYHYOYQXMYHYOIYGYNIZECRYQXMVAZ
      YGYNECVLYRYSECYGYQYNXMYGYQIYFYPIZFDRYNXMVAZYFYPFDVLYTUUAFDYFYPUUAYNYPYFXM
      YNYPYFXMVAYNYPIZXGJYEJUUBYEXGUTZXGJQZYEJQZYCXEYDXFVMUUDUUCYEJUTUUEXGJYEVN
      YEVOVPVQVRVSVTWAWBVDWCWBVDWDWEWFWGWAWHWIVSWJWKWL $.
      $( [26-Nov-2013] $) $( [3-Sep-2009] $)
  $}

  ${
    $d f x F $.  $d f x X $.
    fgf.1 $e |- X = U. F $.
    $( The filter generating class gives a filter for every filter base.
       (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fgf $p |- ( F e. fBas -> ( filGen ` F ) = { x e. ~P X | ( F i^i ~P x ) =/=
    (/) } ) $=
      ( vf cfbas wcel cv cpw cin c0 wne crab cvv cfg cfv wceq cuni uniexg
      syl5eqel pwexg rabexg 3syl unieq syl6eqr pweq syl ineq1 neeq1d rabeqbidv
      df-fg fvmptg mpdan ) BFGZBAHIZJZKLZACIZMZNGZBOPUSQUNCNGURNGUTUNCBRZNDBFST
      CNUAUQAURNUBUCEBEHZUOJZKLZAVBRZIZMUSFNOVBBQZVDUQAVFURVGVECQVFURQVGVEVACVB
      BUDDUEVECUFUGVGVCUPKVBBUOUHUIUJEAUKULUM $.
      $( [26-Nov-2013] $) $( [3-Sep-2009] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y F $.  $d y X $.  $d x y $.
    elfg.1 $e |- X = U. F $.
    $( A condition for elements of a generated filter.  (Contributed by Jeff
       Hankins, 3-Sep-2009.) $)
    elfg $p |- ( F e. fBas -> ( A e. ( filGen ` F ) <-> ( A C_ X
    /\ E. x e. F x C_ A ) ) ) $=
      ( vy cfbas wcel cfg cfv cv cpw cin c0 wne crab wss wrex wa fgf eleq2d
      wceq pweq ineq2d neeq1d elrab cvv wb cuni uniexg syl5eqel elpw2g syl wex
      elin visset elpw anbi2i bitri exbii n0 df-rex 3bitr4i a1i anbi12d syl5bb
      bitrd ) CGHZBCIJZHBCFKZLZMZNOZFDLZPZHZBDQZAKZBQZACRZSZVHVIVOBFCDETUAVPBVN
      HZCBLZMZNOZSVHWAVMWEFBVNVJBUBZVLWDNWFVKWCCVJBUCUDUEUFVHWBVQWEVTVHDUGHWBVQ
      UHVHDCUIUGECGUJUKBDUGULUMWEVTUHVHVRWDHZAUNVRCHZVSSZAUNWEVTWGWIAWGWHVRWCHZ
      SWIVRCWCUOWJVSWHVRBAUPUQURUSUTAWDVAVSACVBVCVDVEVFVG $.
      $( [1-Dec-2010] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.
    $( A filter base is a subset of its generated filter.  (Contributed by Jeff
       Hankins, 3-Sep-2009.) $)
    fbssfg $p |- ( F e. fBas -> F C_ ( filGen ` F ) ) $=
      ( vt vx cfbas wcel cfg cfv cv cuni wss wrex wa elssuni ssid sseq1
      rcla4ev mpan2 jca eqid elfg syl5ibr ssrdv ) ADEZBAAFGZBHZAEZUEUDEUCUEAIZJ
      ZCHZUEJZCAKZLUFUHUKUEAMUFUEUEJZUKUENUJULCUEAUIUEUEOPQRCUEAUGUGSTUAUB $.
      $( [1-Dec-2010] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x y F $.  $d t x y X $.
    fgbas.1 $e |- X = U. F $.
    $( The base set of a generated filter is the base set of the parent base.
       (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fgbas $p |- ( F e. fBas -> X = U. ( filGen ` F ) ) $=
      ( vt vx vy cfbas wcel cfg cfv cuni wss fbssfg uniss syl syl5eqss cv wa
      wex eluni wrex elfg anbi2d ssel2 ancoms adantrr syl6bi 19.23adv syl5bi
      ssrdv eqssd ) AGHZBAIJZKZULBAKZUNCULAUMLUOUNLAMAUMNOPULDUNBDQZUNHUPEQZHZU
      QUMHZRZESULUPBHZEUPUMTULUTVAEULUTURUQBLZFQUQLFAUAZRZRVAULUSVDURFUQABCUBUC
      URVBVAVCVBURVAUQBUPUDUEUFUGUHUIUJUK $.
      $( [1-Dec-2010] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.  $d t x G $.
    $( A bigger base generates a bigger filter. $)
    fgss $p |- ( ( F e. fBas /\ G e. fBas /\ F C_ G ) -> ( filGen ` F ) C_ (
    filGen ` G ) ) $=
      ( vt vx cfbas wcel wss w3a cfg cfv cv cuni wrex wa wi uniss sstr2
      syl5com ssrexv anim12d 3ad2ant3 wb eqid elfg 3ad2ant1 3ad2ant2 3imtr4d
      ssrdv ) AEFZBEFZABGZHZCAIJZBIJZULCKZALZGZDKUOGZDAMZNZUOBLZGZURDBMZNZUOUMF
      ZUOUNFZUKUIUTVDOUJUKUQVBUSVCUKUPVAGUQVBABPUOUPVAQRURDABSTUAUIUJVEUTUBUKDU
      OAUPUPUCUDUEUJUIVFVDUBUKDUOBVAVAUCUDUFUGUH $.
      $( [5-Sep-2009] $)
  $}

  ${
    $d t u v x y F $.  $d t u v x y G $.  $d t u v x y X $.  $d t u v x y Y $.
    $d t u v x y $.
    fbfgss.1 $e |- X = U. F $.
    fbfgss.2 $e |- Y = U. G $.
    $( A condition for a filter to be finer than another involving their filter
       bases.  (Contributed by Jeff Hankins, 3-Sep-2009.) $)
    fbfgss $p |- ( ( F e. fBas /\ G e. fBas /\ X = Y ) -> ( ( filGen ` F ) C_ (
    filGen ` G ) <-> A. x e. F E. y e. G y C_ x ) ) $=
      ( vt vu vv cfbas wcel wceq w3a cfg cfv wss cv wrex wral fbssfg 3ad2ant1
      sseld wa ssel2 wi elfg simpr syl6bi 3ad2ant2 syl5 exp3a syl5d r19.21adv
      sseq2 rexbidv rcla4v adantl simpll3 sseq2d biimpd sstr sseq1 rcla4ev a1d
      sylanr2 jcad exp45 r19.23adv syld ex com23 imp imp3a wb adantr 3imtr4d
      ssrdv impbid ) CLMZDLMZEFNZOZCPQZDPQZRZBSZASZRZBDTZACUAZWDWGWKACWDWICMWIW
      EMZWGWKWDCWEWIWAWBCWERWCCUBUCUDWDWGWMWKWGWMUEWIWFMZWDWKWEWFWIUFWBWAWNWKUG
      WCWBWNWIFRZWKUEWKBWIDFHUHWOWKUIUJUKULUMUNUOWDWLWGWDWLUEZIWEWFWPISZERZJSZW
      QRZJCTZUEZWQFRZKSZWQRZKDTZUEZWQWEMZWQWFMZWPWRXAXGWPXAWRXGWPWTWRXGUGZJCWDW
      LWSCMZWTXJUGZUGWDXKWLXLWDXKWLXLUGWDXKUEZWLWHWSRZBDTZXLXKWLXOUGWDWKXOAWSCW
      IWSNWJXNBDWIWSWHUPUQURUSXMXNXLBDXMWHDMZXNWTXJXMXPXNWTUEZUEZUEZWRXCXFXSWRX
      CXSEFWQWAWBWCXKXRUTVAVBXQXMXPWHWQRZWRXFUGWHWSWQVCXMXPXTUEZUEXFWRYAXFXMXEX
      TKWHDXDWHWQVDVEUSVFVGVHVIVJVKVLVMVNVJVMVOWDXHXBVPZWLWAWBYBWCJWQCEGUHUCVQW
      DXIXGVPZWLWBWAYCWCKWQDFHUHUKVQVRVSVLVT $.
      $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.
    $( A filter generates itself.  (Contributed by Jeff Hankins,
       5-Sep-2009.) $)
    fgid $p |- ( F e. Fil -> ( filGen ` F ) = F ) $=
      ( vt vx cfil wcel cfg cfv cv cuni wss wrex wa cfbas wb filfbas eqid elfg
      syl wi fillsb 3expd com34 r19.23adv com23 imp3a sylbid ssrdv fbssfg
      eqssd ) ADEZAFGZAUJBUKAUJBHZUKEZULAIZJZCHZULJZCAKZLZULAEZUJAMEZUMUSNAOZCU
      LAUNUNPZQRUJUOURUTUJURUOUTUJUQUOUTSCAUJUPAEZUOUQUTUJVDUOUQUTUPULAUNVCTUAU
      BUCUDUEUFUGUJVAAUKJVBAUHRUI $.
      $( [5-Sep-2009] $)
  $}

  ${
    $d u v w y z ph $.  $d u v z F $.  $d u v x y z X $.  $d u w x y z B $.
    fgfild.1 $e |- ( ph -> ( ( B e. fBas /\ U. B C_ X /\ X e. _V ) /\
                       F = { x e. ~P X | E. y e. B y C_ x } ) ) $.
    $( Lemma for ~ fgfil . $)
    fgfild $p |- ( ph -> ( F e. Fil /\ X = U. F ) ) $=
      ( vz vu vv vw cfil wcel cuni wceq cv wss wrex cpw crab wa cfbas cvv w3a
      simprd eleq2d sseq2 rexbidv elrab visset elpw anbi1i bitri syl6bb simpld
      simp3d wsbc wex c0 wne simp1d fbasne0 syl n0 sylib elssuni simp2d sstr2
      syl5com syl5 ancld eximdv mpd df-rex sylibr wb sbcieg mpbird wn 0nelfb
      0ex sbcie ss0 eleq1 biimpac r19.23aiva sylbi nsyl wi sstr expcom reximdv
      3ad2ant3 3imtr4g cin fbasssin 3expib ss2in com12 syl6 exp5c imp31
      impancom r19.23adv ex r19.23adva imp3a 3ad2ant1 sseq1 cbvrexv anbi12i
      inex1 isfild isfildu jca ) AELMFENOACPZHPZQZCDRZHIJFEAXQEMXQXPBPZQZCDRZBF
      SZTZMZXQFQZXSUAZAEYDXQADUBMZDNZFQZFUCMZUDZEYDOZGUEUFYEXQYCMZXSUAYGYBXSBXQ
      YCXTXQOYAXRCDXTXQXPUGUHUIYNYFXSXQFHUJUKULUMUNZAYHYJYKAYLYMGUOZUPZAXSHFUQZ
      XPFQZCDRZAXPDMZYSUAZCURZYTAUUACURZUUCADUSUTZUUDAYHUUEAYHYJYKYPVAZDVBVCCDV
      DVEAUUAUUBCAUUAYSUUAXPYIQZAYSXPDVFAYJUUGYSAYHYJYKYPVGXPYIFVHVIVJVKVLVMYSC
      DVNVOAYKYRYTVPYQXSYTHFUCXQFOXRYSCDXQFXPUGUHVQVCVRZAUSDMZXSHUSUQZAYHUUIVSU
      UFDVTVCUUJXPUSQZCDRZUUIXSUULHUSWAXQUSOXRUUKCDXQUSXPUGUHWBUUKUUICDUUKUUAUU
      IUUKXPUSOUUAUUIVPXPWCXPUSDWDVCWEWFWGWHAIPZFQZJPZUUMQZUDXPUUOQZCDRZXPUUMQZ
      CDRZXSHUUOUQZXSHUUMUQZUUPAUURUUTWIUUNUUPUUQUUSCDUUQUUPUUSXPUUOUUMWJWKWLWM
      XSUURHUUOJUJXQUUOOXRUUQCDXQUUOXPUGUHWBZXSUUTHUUMIUJZXQUUMOXRUUSCDXQUUMXPU
      GUHWBZWNAUUNUUOFQZUDXQUUMQZHDRZKPZUUOQZKDRZUAZXPUUMUUOWOZQZCDRZUVBUVAUAXS
      HUVMUQAUUNUVLUVOWIUVFAUVHUVKUVOAUVGUVKUVOWIZHDAXQDMZUAZUVGUVPUVRUVGUAUVJU
      VOKDUVRUVIDMZUVGUVJUVOWIZAUVQUVSUVGUVTWIAUVQUVSUVGUVJUVOAUVQUVSUAZXPXQUVI
      WOZQZCDRZUVGUVJUAZUVOWIAYHUWAUWDWIUUFYHUVQUVSUWDCXQUVIDWPWQVCUWEUWDUVOUWE
      UWBUVMQZUWDUVOWIXQUUMUVIUUOWRUWFUWCUVNCDUWCUWFUVNXPUWBUVMVHWSWLVCWSWTXAXB
      XCXDXEXFXGXHUVBUVHUVAUVKUVBUUTUVHUVEUUSUVGCHDXPXQUUMXIXJUMUVAUURUVKUVCUUQ
      UVJCKDXPUVIUUOXIXJUMXKXSUVOHUVMUUMUUOUVDXLXQUVMOXRUVNCDXQUVMXPUGUHWBWNXMA
      XSHFEYOYQUUHXNXO $.
      $( [30-Nov-2013] $)
  $}

  ${
    $d x y z F $.
    $( A generated filter is a filter.  (Contributed by Jeff Hankins,
       3-Sep-2009.)  (Revised by Mario Carneiro, 28-Nov-2013.) $)
    fgfil $p |- ( F e. fBas -> ( filGen ` F ) e. Fil ) $=
      ( vx vy vz cfbas wcel cfg cfv cfil cuni wceq wss cvv w3a cv wrex cpw
      crab id ssid a1i uniexg 3jca wa eqid elfg sseq2 rexbidv elrab visset
      elpw anbi1i bitri syl6bbr eqrdv jca fgfild simpld ) AEFZAGHZIFAJZUTJKUSBC
      AUTVAUSUSVAVALZVAMFZNUTCOZBOZLZCAPZBVAQZRZKUSUSVBVCUSSVBUSVATUAAEUBUCUSDU
      TVIUSDOZUTFVJVALZVDVJLZCAPZUDZVJVIFZCVJAVAVAUEUFVOVJVHFZVMUDVNVGVMBVJVHVE
      VJKVFVLCAVEVJVDUGUHUIVPVKVMVJVADUJUKULUMUNUOUPUQUR $.
      $( [28-Nov-2013] $) $( [3-Sep-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z F $.  $d x y X $.
    extbas.1 $e |- X = U. F $.
    $( A way to extend the base set of a filter.  (Contributed by Jeff Hankins,
       6-Sep-2009.) $)
    extbas1 $p |- ( ( F e. fBas /\ X C_ A ) -> ( F u. { A } ) e. fBas ) $=
      ( vz vx vy cfbas wcel wss wa cvv csn cun cv cin wrex wral ssun1 simprl
      wceq cuni elssuni syl6sseqr adantr sstr syl2anc elsn biimpi ad2antll
      sseqtr4d df-ss sylib eqimss2 syl sseq1 rcla4ev syl11anc ssrexv mpsyl
      r19.21aivva wb c0 wne id fbdmn0 ssn0 cfil snfil filfbas anasss fbun
      syldan mpbird expr wn wi snprc uneq2 sylbi un0 syl6req eleq1d biimpcd
      pm2.61d ) BHIZCAJZKZALIZBAMZNZHIZWFWGWIWLWFWGWIKZKZWLEOZFOZGOZPZJZEWKQZGW
      JRFBRZWNWTFGBWJBWKJWNWPBIZWQWJIZKZKZWSEBQZWTBWJSXEXBWPWRJZXFWNXBXCTXEWRWP
      UAZXGXEWPWQJXHXEWPAWQXDWPCJZWGWPAJWNXBXIXCXBWPBUBCWPBUCDUDUEWFWGWITWPCAUF
      UGXCWQAUAZWNXBXCXJGAUHUIUJUKWPWQULUMWPWRUNUOWSXGEWPBWOWPWRUPUQURWSEBWKUSU
      TVAWFWMWJHIZWLXAVBWFWGWIXKWIWIAVCVDZXKWHWIVEWGWGCVCVDXLWFWGVEBCDVFCAVGUGW
      IXLKWJVHIXKALVIWJVJUOUGVKFGEBWJVLVMVNVOWFWIVPZWLVQWGXMWFWLXMBWKHXMWKBVCNZ
      BXMWJVCUAWKXNUAAVRWJVCBVSVTBWAWBWCWDUEWE $.
      $( [28-Nov-2013] $) $( [6-Sep-2009] $)

    $( The base set of an extended filter.  (Contributed by Jeff Hankins,
       7-Sep-2009.) $)
    extbas2 $p |- ( ( X C_ A /\ A e. B ) -> U. ( F u. { A } ) =
      A ) $=
      ( wss wcel wa csn cun cuni uniun uneq1i eqtr4i unisng uneq2d ssid jctr
      unss sylib ssun2 a1i eqssd sylan9eqr syl5eq ) DAFZABGZHCAIZJKZDUHKZJZAUIC
      KZUJJUKCUHLDULUJEMNUGUFUKDAJZAUGUJADABOPUFUMAUFUFAAFZHUMAFUFUNAQRDAASTAUM
      FUFADUAUBUCUDUE $.
      $( [1-Dec-2010] $) $( [7-Sep-2009] $)
  $}

  ${
    $d r s u v w x y z B $.  $d r s u v w z C $.  $d r s u v w x y z F $.
    $d r s u v w x X $.
    filrn.1 $e |- X = U. B $.
    filrn.2 $e |- C = { y | E. x e. B y = ( F " x ) } $.
    $( Given a filter on a domain, produce a filter on the range.  (Contributed
       by Jeff Hankins, 7-Sep-2009.) $)
    filrn $p |- ( ( B e. fBas /\ F Fn X ) -> C e. fBas ) $=
      ( vz vr vs vu vv vw cfbas wcel wfn wa c0 wne wnel cv cin wss wrex wral
      w3a cima cmpt crn wceq cab eqid rnmpt eqtr4i a1i cdm wfun cvv fnfun
      adantl funimaexg r19.21aiva dmmpt 3syl fbasne0 adantr eqnetrd dm0rn0
      necon3bii sylib wn cuni wi elssuni 0nelfb eleq1 notbid syl5ibrcom con2d
      jcad fndm syl6eq sseq2d biimpar sseqin2 eqeq1d biimpd con3d expimpd
      eqcom imadisj bitri notbii syl6ibr syld r19.21aiv eleq2i 0ex eqeq1
      rexbidv elab bitr2i ralnex df-nel 3bitr4i visset imaeq2 eqeq2d cbvrexv
      3bitri anbi12i reeanv bitr4i fbasssin 3expb ad2ant2r rcla4ev mpan2
      ad2antrl wb funimaex elabg syl5bb ad2antrr mpbird imass2 ad2antll inss1
      ax-mp inss2 ssini ineq12 ad2antlr syl5sseqr sstrd sseq1 syl11anc
      adantlrl exp32 r19.23adv mpd r19.23advv syl5bi r19.21aivv 3jca abrexexg
      syl5eqel isfbas2 syl ) COPZEFQZRZDOPZDSTZSDUAZIUBZJUBZKUBZUCZUDZIDUEZKDUF
      JDUFZUGZUUMUUOUUPUVCUUMDACEAUBZUHZUIZUJZSDUVHUKUUMDBUBZUVFUKZACUEZBULZUVH
      HABCUVFUVGUVGUMUNUOUPUUMUVGUQZSTUVHSTUUMUVMCSUUMEURZUVFUSPZACUFUVMCUKUULU
      VNUUKFEUTZVAUVNUVOACEUVECVBVCACUVFUSVDVEUUKCSTUULCVFVGVHUVMSUVHSUVGVIVJVK
      VHUUMSUVFUKZVLZACUFZUUPUUMUVRACUUMUVECPZUVECVMZUDZUVESUKZVLZRZUVRUUMUVTUW
      BUWDUVTUWBVNUUMUVECVOUPUUKUVTUWDVNUULUUKUWCUVTUUKUVTVLUWCSCPZVLCVPUWCUVTU
      WFUVESCVQVRVSVTVGWAUUMUWEEUQZUVEUCZSUKZVLZUVRUUMUWBUWDUWJUUMUWBRZUWIUWCUW
      KUWIUWCUWKUWHUVESUWKUVEUWGUDZUWHUVEUKUUMUWLUWBUUMUWGUWAUVEUUMUWGFUWAUULUW
      GFUKUUKFEWBVAGWCWDWEUVEUWGWFVKWGWHWIWJUVQUWIUVQUVFSUKUWISUVFWKEUVEWLWMWNW
      OWPWQUVQACUEZVLSDPZVLUVSUUPUWMUWNUWNSUVLPUWMDUVLSHWRUVKUWMBSWSUVISUKUVJUV
      QACUVISUVFWTXAXBXCWNUVQACXDSDXEXFVKUUMUVBJKDDUURDPZUUSDPZRZUURELUBZUHZUKZ
      UUSEMUBZUHZUKZRZMCUELCUEZUUMUVBUWQUWTLCUEZUXCMCUEZRUXEUWOUXFUWPUXGUWOUURU
      VLPUURUVFUKZACUEZUXFDUVLUURHWRUVKUXIBUURJXGUVIUURUKUVJUXHACUVIUURUVFWTXAX
      BUXHUWTALCUVEUWRUKUVFUWSUURUVEUWREXHXIXJXKUWPUUSUVLPUUSUVFUKZACUEZUXGDUVL
      UUSHWRUVKUXKBUUSKXGUVIUUSUKUVJUXJACUVIUUSUVFWTXAXBUXJUXCAMCUVEUXAUKUVFUXB
      UUSUVEUXAEXHXIXJXKXLUWTUXCLMCCXMXNUUMUXDUVBLMCCUUMUWRCPZUXACPZRZUXDUVBUUM
      UXNUXDRRZNUBZUWRUXAUCZUDZNCUEZUVBUUKUXNUXSUULUXDUUKUXLUXMUXSNUWRUXACXOXPX
      QUXOUXRUVBNCUXOUXPCPZUXRUVBUUMUXDUXTUXRRZUVBUXNUUMUXDRZUYARZEUXPUHZDPZUYD
      UUTUDZUVBUYCUYEUYDUVFUKZACUEZUXTUYHUYBUXRUXTUYDUYDUKZUYHUYDUMUYGUYIAUXPCU
      VEUXPUKUVFUYDUYDUVEUXPEXHXIXRXSXTUUMUYEUYHYAZUXDUYAUULUYJUUKUYEUYDUVLPZUU
      LUYHDUVLUYDHWRUULUVNUYDUSPUYKUYHYAUVPEUXPNXGYBUVKUYHBUYDUSUVIUYDUKUVJUYGA
      CUVIUYDUVFWTXAYCVEYDVAYEYFUYCUYDEUXQUHZUUTUXRUYDUYLUDUYBUXTUXPUXQEYGYHUYC
      UWSUXBUCZUYLUUTUYLUWSUXBUXQUWRUDUYLUWSUDUWRUXAYIUXQUWREYGYJUXQUXAUDUYLUXB
      UDUWRUXAYKUXQUXAEYGYJYLUXDUUTUYMUKUUMUYAUURUWSUUSUXBYMYNYOYPUVAUYFIUYDDUU
      QUYDUUTYQXRYRYSYTUUAUUBYTUUCUUDUUEUUFUUKUUNUVDYAZUULUUKDUSPUYNUUKDUVLUSHA
      BCUVFOUUGUUHJKIUSDUUIUUJVGYF $.
      $( [28-Nov-2013] $) $( [7-Sep-2009] $)
  $}

  ${
    $d f g h x y F $.  $d f g x y X $.
    $( The union of a nonempty set of filters with a common base and closed
       under pairwise union is a filter.  (Contributed by Mario Carneiro,
       28-Nov-2013.) $)
    filuni $p |- ( ( F C_ Fil /\ F =/= (/) /\ A. f e. F ( U. f = X /\
           A. g e. F ( f u. g ) e. F ) ) -> ( U. F e. Fil /\ X = U. U. F ) ) $=
      ( vx vy vh cfil wss c0 wne cv cuni wceq cun wcel wral wa w3a wrex eluni2
      wi r19.29 simpl elssuni sseq2 biimpa syl2an r19.23aivw syl ex 3ad2ant3
      syl5bi pm4.71rd cvv r19.2z visset uniex eleq1 mpbii adantr 3adant1 wsbc
      ssel2 eqid filusb wb syl5ibcom ralimdva sylan9 3impia sylibr sbcel1gv
      mpbird wn filesn r19.21aiva 3ad2ant1 0ex ax-mp bitri notbii ralnex
      bitr4i simp13 simp1 simprrr simpl2 simprll adantl sseqtr4d simpl3 fillsb
      imp syl13anc expr reximdva syl3an1 syld sbid clelsb3 3imtr4g cin anim1d
      an32 simplrr elun1 elun2 anim12i eleq2 anbi12d rcla4ev sylan2 an12s
      r19.23adva syl9r impr sylan2b r19.23aiva simp11 filint 3expib syl5
      anbi12i inex1 isfild isfildu jca ) CHIZCJKZALZMZDNZYKBLZOZCPZBCQZRZACQZSZ
      CMZHPDUUAMNYTELZUUAPZEFEDUUAYTUUCUUBDIZUUCUUBYKPZACTZYTUUDAUUBCUAZYSYIUUF
      UUDUBYJYSUUFUUDYSUUFRYRUUERZACTZUUDYRUUEACUCZUUHUUDACYRYMUUBYLIZUUDUUEYMY
      QUDUUBYKUEYMUUKUUDYLDUUBUFUGUHUIUJUKULUMUNZYJYSDUOPZYIYJYSRYRACTUUMYRACUP
      YRUUMACYMUUMYQYMYLUOPUUMYKAUQURYLDUOUSUTVAUIUJVBZYTUUCEDVCZDUUAPZYTDYKPZA
      CTZUUPYIYJYSUURYIYSUUQACQZYJUURYIYRUUQACYIYKCPZRZYLYKPZYRUUQUVAYKHPZUVBCH
      YKVDZYKYLYLVEZVFUJYMUVBUUQVGYQYLDYKUSVAVHVIYJUUSUURUUQACUPUKVJVKADCUAVLYT
      UUMUUOUUPVGUUNEDUUAUOVMUJVNZYTJYKPZVOZACQZUUCEJVCZVOZYIYJUVIYSYIUVHACUVAU
      VCUVHUVDYKVPUJVQVRUVKUVGACTZVOUVIUVJUVLUVJJUUAPZUVLJUOPUVJUVMVGVSEJUUAUOV
      MVTAJCUAWAWBUVGACWCWDVLYTFLZDIZUUBUVNIZSZUUFUVNYKPZACTZUUCEUUBVCZUUCEUVNV
      CZUVQUUFUUIUVSUVQYSUUFUUIUBYIYJYSUVOUVPWEYSUUFUUIUUJUKUJYTYIUVOUVPUUIUVSU
      BYIYJYSWFYIUVOUVPSZUUHUVRACUWBUUTUUHUVRUWBUUTUUHRZRZUVCUUEUVNYLIZUVPUVRUW
      BYIUUTUVCUWCYIUVOUVPWFUUTUUHUDUVDUHUWBUUTYRUUEWGUWDUVNDYLYIUVOUVPUWCWHUWC
      YMUWBUUTYMYQUUEWIWJWKYIUVOUVPUWCWLUVCUUEUWEUVPSUVRUUBUVNYKYLUVEWMWNWOWPWQ
      WRWSUVTUUCUUFUUCEWTZUUGWAUWAUVNUUAPUVSFEUUAXAAUVNCUAWAZXBYTUVOUUDSZUVSUUB
      YNPZBCTZRZUVNUUBXCZGLZPZGCTZUWAUVTRUUCEUWLVCZUWHUWKYRUVRRZACTZUWJRZUWOUWH
      UVSUWRUWJUWHYSUVSUWRUBYIYJYSUVOUUDWEYSUVSUWRYRUVRACUCUKUJXDUWSUVNUWMPZUUB
      UWMPZRZGCTZUWHUWOUWRUWJUXCUWQUWJUXCUBZACUWQUUTYMUVRRZYQRUXDYMYQUVRXEUUTUX
      EYQUXDYQUWJYPUWIRZBCTZUUTUXERZUXCYQUWJUXGYPUWIBCUCUKUXHUXFUXCBCUXHYNCPZRU
      VRUXFUXCUBUUTYMUVRUXIXFUVRUXFUXCYPUVRUWIUXCUVRUWIRYPUVNYOPZUUBYOPZRZUXCUV
      RUXJUWIUXKUVNYKYNXGUUBYNYKXHXIUXBUXLGYOCUWMYONUWTUXJUXAUXKUWMYOUVNXJUWMYO
      UUBXJXKXLXMXNUKUJXOXPXQXRXSWNUWHYIUXCUWOUBYIYJYSUVOUUDXTYIUXBUWNGCYIUWMCP
      RUWMHPZUXBUWNUBCHUWMVDUXMUWTUXAUWNUVNUUBUWMYAYBUJWQUJYCWSUWAUVSUVTUWJUWGU
      VTUUCUWJUWFBUUBCUAWAYDUWPUWLUUAPZUWOUWLUOPUWPUXNVGUVNUUBFUQYEEUWLUUAUOVMV
      TGUWLCUAWAXBYFYTUUCEDUUAUULUUNUVFYGYH $.
      $( [28-Nov-2013] $)
  $}


