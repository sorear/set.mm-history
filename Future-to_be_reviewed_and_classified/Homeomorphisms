$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Homeo $. $( Class of all homeomorphisms. $)
  $c ~= $. $( The relation "is homeomorph to." $)

  $( Extend class notation with the class of all homeomorphisms. $)
  chomeosm $a class Homeo $.

  $( Extend class notation with the relation "is homeomorph to.". $)
  chomeo $a class ~= $.

  ${
    $d j k z f x $.
    $( Function returning all the homeomorphisms from topology ` j ` to
       topology ` k ` . $)
    df-homeo $a |- Homeo = { <. <. j , k >. , z >. | ( ( j e. Top /\
     k e. Top ) /\ z = { f | ( f : U. j -1-1-onto-> U. k /\
     A. x e. j ( f " x ) e. k /\
     A. x e. k ( `' f " x ) e. j ) } ) } $.
  $}

  ${
    $d x y f $.
    $( Definition of the relation ` x ` is homeomorph to ` y ` . $)
    df-hmph $a |- ~= = { <. x , y >. | ( x e. Top /\ y e. Top /\
      E. f f e. ( x Homeo y ) ) } $.
  $}

  ${
    $d J f j k x z $.  $d K f j k x z $.  $d X f j k z $.  $d Y f k z $.
    homeofval.1 $e |- X = U. J $.
    homeofval.2 $e |- Y = U. K $.
    $( The set of all the homeomorphisms between two topologies.  (Contributed
       by FL, 20-Feb-2007.) $)
    homeofval $p |- ( ( J e. Top /\ K e. Top ) -> ( J Homeo K ) =
    { f | ( f : X -1-1-onto-> Y /\ A. x e. J ( f " x ) e. K /\
    A. x e. K ( `' f " x ) e. J ) } ) $=
      ( vj vk vz ctop wcel cv wf1o cima wral ccnv w3a cab cvv chomeosm co wceq
      wf wss wa ssexg wi f1of 3ad2ant1 a1i ss2abdv mapex cuni uniexg syl5eqel
      syl2an sylanc wb unieq syl6eqr f1oeq2 syl raleq eleq2 ralbidv 3anbi123d
      abbidv f1oeq3 df-homeo oprabval2g mpd3an3 ) CLMZDLMZEFBNZOZVPANZPZDMZACQZ
      VPRVRPZCMZADQZSZBTZUAMZCDUBUCWFUDWFEFVPUEZBTZUFWIUAMZWGVNVOUGZWFWIUAUHWKW
      EWHBWEWHUIWKVQWAWHWDEFVPUJUKULUMEUAMFUAMWJVNVOEFUAUABUNVNCUOZUAECLUPGUQVO
      DUOZUAFDLUPHUQURUSIJKCDLLINZUOZJNZUOZVPOZVSWPMZAWNQZWBWNMZAWPQZSZBTWFUBEW
      QVPOZWSACQZWCAWPQZSZBTUAWNCUDZXCXGBXHWRXDWTXEXBXFXHWOEUDWRXDUTXHWOWLEWNCV
      AGVBWOEWQVPVCVDWSAWNCVEXHXAWCAWPWNCWBVFVGVHVIWPDUDZXGWEBXIXDVQXEWAXFWDXIW
      QFUDXDVQUTXIWQWMFWPDVAHVBWQFEVPVJVDXIWSVTACWPDVSVFVGWCAWPDVEVHVIAKBIJVKVL
      VM $.
      $( [1-Dec-2010] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F f x $.  $d J f x $.  $d K f x $.  $d X f $.  $d Y f $.
    ishomeo.1 $e |- X = U. J $.
    ishomeo.2 $e |- Y = U. K $.
    $( The predicate F is a homeomorphism between topology ` J ` and topology
       ` K ` .  Based on Bourbaki TG I.2.  (Contributed by FL, 20-Feb-2007.) $)
    ishomeo $p |- ( ( J e. Top /\ K e. Top /\ F e. A ) ->
      ( F e. ( J Homeo K ) <-> ( F : X -1-1-onto-> Y /\
        A. x e. J ( F " x ) e. K /\
        A. x e. K ( `' F " x ) e. J ) ) ) $=
      ( vf ctop wcel w3a chomeosm co cv wf1o cima wral ccnv cab wceq homeofval
      3adant3 eleq2d wb f1oeq1 imaeq1 eleq1d ralbidv cnveq imaeq1d 3anbi123d
      elabg 3ad2ant3 bitrd ) DKLZEKLZCBLZMZCDENOZLCFGJPZQZVBAPZRZELZADSZVBTZVDR
      ZDLZAESZMZJUAZLZFGCQZCVDRZELZADSZCTZVDRZDLZAESZMZUTVAVMCUQURVAVMUBUSAJDEF
      GHIUCUDUEUSUQVNWCUFURVLWCJCBVBCUBZVCVOVGVRVKWBFGVBCUGWDVFVQADWDVEVPEVBCVD
      UHUIUJWDVJWAAEWDVIVTDWDVHVSVDVBCUKULUIUJUMUNUOUP $.
      $( [1-Dec-2010] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    hmeomap.1 $e |- X = U. J $.
    hmeomap.2 $e |- Y = U. K $.
    $( A homeomorphism is a 1-1-onto mapping.  (Contributed by FL,
       5-Mar-2007.) $)
    hmeomap $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> F : X -1-1-onto-> Y ) ) $=
      ( vx ctop wcel wa chomeosm co wf1o wi w3a cv cima wral ccnv ishomeo
      biimpa 3simp1d 3exp1 imp pm2.43d ) BIJZCIJZKABCLMZJZDEANZUGUHUJUJUKOOUGUH
      UJUJUKUGUHUJPZUJKUKAHQZRCJHBSZATUMRBJHCSZULUJUKUNUOPHUIABCDEFGUAUBUCUDUEU
      F $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The image of an open set by the converse of a homeomorphism is an open
       set.  (Contributed by FL, 5-Mar-2007.) $)
    hmeocna $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. K ( `' F " x ) e. J ) ) $=
      ( ctop wcel wa chomeosm co ccnv cv cima wral wi w3a cuni wf1o eqid
      ishomeo biimpa 3simp3d 3exp1 imp pm2.43d ) CEFZDEFZGBCDHIZFZBJAKZLCFADMZU
      EUFUHUHUJNNUEUFUHUHUJUEUFUHOZUHGCPZDPZBQZBUILDFACMZUJUKUHUNUOUJOAUGBCDULU
      MULRUMRSTUAUBUCUD $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The image of an open set by a homeomorphism is an open set.
       (Contributed by FL, 5-Mar-2007.) $)
    hmeocnb $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. J ( F " x ) e. K ) ) $=
      ( ctop wcel wa chomeosm co cv cima wral wi w3a cuni wf1o ccnv eqid
      ishomeo biimpa 3simp2d 3exp1 imp pm2.43d ) CEFZDEFZGBCDHIZFZBAJZKDFACLZUE
      UFUHUHUJMMUEUFUHUHUJUEUFUHNZUHGCOZDOZBPZUJBQUIKCFADLZUKUHUNUJUONAUGBCDULU
      MULRUMRSTUAUBUCUD $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.  $d X x $.  $d Y x $.
    hmeobc.1 $e |- X = U. J $.
    hmeobc.2 $e |- Y = U. K $.
    $( F e. A is useless see ~ iscn $)
    $( A homeomorphism is a bicontinuous bijection.  (Contributed by FL,
       1-Sep-2008.) $)
    hmeobc $p |- ( ( J e. Top /\ K e. Top /\ F e. A ) -> ( F e. ( J Homeo K )
      <-> ( F : X -1-1-onto-> Y /\
          F e. ( J Cn K ) /\ `' F e. ( K Cn J ) ) ) ) $=
      ( vx ctop wcel w3a wf1o cv cima wral ccnv wf wa chomeosm co ccn 3simp1
      adantl f1of 3ad2ant1 3simp3 jca f1ocnv syl imacnvcnv eqcomi eleq1i
      ralbii biimpi 3ad2ant2 3jca wi wrel frel wceq a1i eleq1d ralbidv biimpcd
      com12 a1d imp 3imp simpr2r impbida ishomeo wb iscn 3adant3 ancoms
      3anbi23d 3bitr4d ) CJKZDJKZBAKZLZEFBMZBINZOZDKZICPZBQZWDOCKIDPZLZWCEFBRZW
      ISZFEWHRZWHQWDOZDKZICPZSZLZBCDTUAKWCBCDUBUAKZWHDCUBUAKZLWBWJWRWBWJSZWCWLW
      QWJWCWBWCWGWIUCUDXAWKWIWJWKWBWCWGWKWIEFBUEUFUDWJWIWBWCWGWIUGUDUHXAWMWPWJW
      MWBWCWGWMWIWCFEWHMWMEFBUIFEWHUEUJUFUDWJWPWBWGWCWPWIWGWPWFWOICWEWNDWNWEBWD
      UKZULUMUNUOUPUDUHUQWBWRSWCWGWIWRWCWBWCWLWQUCUDWRWGWBWCWLWQWGWLWQWGURZURWC
      WKWIXCWKBUSZWIXCUREFBUTXDXCWIWQXDWGWPXDWGURWMXDWPWGXDWOWFICXDWNWEDWNWEVAX
      DXBVBVCVDVEUDVFVGUJVHVBVIUDWKWIWCWQWBVJUQVKIABCDEFGHVLWBWSWLWTWQWCVSVTWSW
      LVMWAIBCDEFGHVNVOVSVTWTWQVMZWAVTVSXEIWHDCFEHGVNVPVOVQVR $.
      $( [1-Dec-2010] $) $( [23-Aug-2008] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The converse of a homeomorphism is a homeomorphism.  (Contributed by FL,
       5-Mar-2007.) $)
    cnvhmpha $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> `' F e. ( K Homeo J ) ) ) $=
      ( vx chomeosm co wcel ctop wa ccnv cvv wi cnvexg w3a cuni wf1o cv cima
      wral eqid hmeomap f1ocnv syl6 imp hmeocna hmeocnb wceq wrel f1orel
      dfrel2 sylib adantr imaeq1d eleq1d ralbidva mpbird 3jca ex ancoms
      3adant3 ishomeo sylibrd 3exp impcom com3l mpcom com12 ) ABCEFZGZBHGZCHGZI
      ZAJZCBEFGZVMKGZVIVLVNLAVHMVLVOVIVNVKVJVOVIVNLZLVKVJVOVPVKVJVONVICOZBOZVMP
      ZVMDQZRBGDCSZVMJZVTRZCGZDBSZNZVNVKVJVIWFLZVOVJVKWGVLVIWFVLVIIZVSWAWEVLVIV
      SVLVIVRVQAPZVSABCVRVQVRTZVQTZUAZVRVQAUBUCUDVLVIWADABCUEUDWHWEAVTRZCGZDBSZ
      VLVIWODABCUFUDWHWDWNDBWHVTBGZIZWCWMCWQWBAVTWHWBAUGZWPVLVIWRVLVIWIWRWLWIAU
      HWRVRVQAUIAUJUKUCUDULUMUNUOUPUQURUSUTDKVMCBVQVRWKWJVAVBVCVDVEVFVG $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d J f j k $.  $d K f j k $.
    $( Express the predicate ` J ` is homeomorph to ` K ` .  (Contributed by
       FL, 20-Feb-2007.) $)
    hmph $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K <->
      E. f f e. ( J Homeo K ) ) ) $=
      ( vj vk ctop wcel wa chomeo wbr cv chomeosm co wex w3a wceq eleq1 opreq1
      eleq2d exbidv 3anbi13d bicomd opreq2 eqcoms 3anbi23d df-3an syl5rbbr
      df-hmph brabg bianabs ) BFGZCFGZHZBCIJAKZBCLMZGZANZDKZFGZEKZFGZUNURUTLMZG
      ZANZOUKVAUNBUTLMZGZANZOZUMUQHZDEBCFFIURBPZUSUKVDVGVAURBFQVJVCVFAVJVBVEUNU
      RBUTLRSTUAUTCPZUKULUQOVHVIVKULVAUQVGUKVKVAULUTCFQUBVKUPVFAVKUOVEUNUOVEPCU
      TCUTBLUCUDSTUEUKULUQUFUGDEAUHUIUJ $.
      $( [1-Dec-2010] $) $( [14-Feb-2007] $)
  $}

