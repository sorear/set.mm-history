$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Homeo $. $( Class of all homeomorphisms. $)
  $c ~= $. $( The relation "is homeomorph to." $)

  $( Extend class notation with the class of all homeomorphisms. $)
  chomeosm $a class Homeo $.

  $( Extend class notation with the relation "is homeomorph to.". $)
  chomeo $a class ~= $.

  ${
    $d j k f x $.
    $( Function returning all the homeomorphisms from topology ` j ` to
       topology ` k ` . $)
    df-homeo $a |- Homeo = ( j e. Top , k e. Top |->
            { f | ( f : U. j -1-1-onto-> U. k /\ A. x e. j ( f " x ) e. k /\
                    A. x e. k ( `' f " x ) e. j ) } ) $.
  $}

  ${
    $d x y f $.
    $( Definition of the relation ` x ` is homeomorph to ` y ` . $)
    df-hmph $a |- ~= = { <. x , y >. | ( x e. Top /\ y e. Top /\
      E. f f e. ( x Homeo y ) ) } $.
  $}

  ${
    $d J f j k x $.  $d K f k x $.  $d X f j k $.  $d Y f k $.
    homeofval.1 $e |- X = U. J $.
    homeofval.2 $e |- Y = U. K $.
    $( The set of all the homeomorphisms between two topologies.  (Contributed
       by FL, 20-Feb-2007.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    homeofval $p |- ( ( J e. Top /\ K e. Top ) -> ( J Homeo K ) =
    { f | ( f : X -1-1-onto-> Y /\ A. x e. J ( f " x ) e. K /\
    A. x e. K ( `' f " x ) e. J ) } ) $=
      ( vj vk ctop wcel cv wf1o wral w3a cab cvv wceq cuni cima ccnv co wa cmap
      chomeosm wss wf 3ad2ant1 wb uniexg syl5eqel elmapg syl2anc syl5ibr abssdv
      f1of oprex ssexg sylancl unieq syl6eqr f1oeq2 syl raleq ralbidv 3anbi123d
      eleq2 abbidv f1oeq3 df-homeo ovmpt2g mpd3an3 ) CKLZDKLZEFBMZNZVPAMZUAZDLZ
      ACOZVPUBVRUAZCLZADOZPZBQZRLZCDUFUCWFSVNVOUDZWFFEUEUCZUGWIRLWGWHWEBWIWEVPW
      ILZWHEFVPUHZVQWAWKWDEFVPUQUIVOFRLERLWJWKUJVNVOFDTZRHDKUKULVNECTZRGCKUKULF
      EVPRRUMUNUOUPFEUEURWFWIRUSUTIJCDKKIMZTZJMZTZVPNZVSWPLZAWNOZWBWNLZAWPOZPZB
      QWFUFEWQVPNZWSACOZWCAWPOZPZBQRWNCSZXCXGBXHWRXDWTXEXBXFXHWOESWRXDUJXHWOWME
      WNCVAGVBWOEWQVPVCVDWSAWNCVEXHXAWCAWPWNCWBVHVFVGVIWPDSZXGWEBXIXDVQXEWAXFWD
      XIWQFSXDVQUJXIWQWLFWPDVAHVBWQFEVPVJVDXIWSVTACWPDVSVHVFWCAWPDVEVGVIABIJVKV
      LVM $.
      $( [15-Dec-2013] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F f x $.  $d J f x $.  $d K f x $.  $d X f $.  $d Y f $.
    ishomeo.1 $e |- X = U. J $.
    ishomeo.2 $e |- Y = U. K $.
    $( The predicate F is a homeomorphism between topology ` J ` and topology
       ` K ` .  Based on Bourbaki TG I.2.  (Contributed by FL, 20-Feb-2007.) $)
    ishomeo $p |- ( ( J e. Top /\ K e. Top /\ F e. A ) ->
      ( F e. ( J Homeo K ) <-> ( F : X -1-1-onto-> Y /\
        A. x e. J ( F " x ) e. K /\
        A. x e. K ( `' F " x ) e. J ) ) ) $=
      ( vf ctop wcel w3a cv wf1o cima wral ccnv wceq eleq1d chomeosm co 3adant3
      cab homeofval eleq2d f1oeq1 imaeq1 ralbidv cnveq 3anbi123d elabg 3ad2ant3
      wb imaeq1d bitrd ) DKLZEKLZCBLZMZCDEUAUBZLCFGJNZOZVBANZPZELZADQZVBRZVDPZD
      LZAEQZMZJUDZLZFGCOZCVDPZELZADQZCRZVDPZDLZAEQZMZUTVAVMCUQURVAVMSUSAJDEFGHI
      UEUCUFUSUQVNWCUNURVLWCJCBVBCSZVCVOVGVRVKWBFGVBCUGWDVFVQADWDVEVPEVBCVDUHTU
      IWDVJWAAEWDVIVTDWDVHVSVDVBCUJUOTUIUKULUMUP $.
      $( [1-Dec-2010] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    hmeomap.1 $e |- X = U. J $.
    hmeomap.2 $e |- Y = U. K $.
    $( A homeomorphism is a 1-1-onto mapping.  (Contributed by FL,
       5-Mar-2007.) $)
    hmeomap $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> F : X -1-1-onto-> Y ) ) $=
      ( vx ctop wcel wa chomeosm co wf1o wi w3a cv cima wral ccnv biimpa simp1d
      ishomeo 3exp1 imp pm2.43d ) BIJZCIJZKABCLMZJZDEANZUGUHUJUJUKOOUGUHUJUJUKU
      GUHUJPZUJKUKAHQZRCJHBSZATUMRBJHCSZULUJUKUNUOPHUIABCDEFGUCUAUBUDUEUF $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The image of an open set by the converse of a homeomorphism is an open
       set.  (Contributed by FL, 5-Mar-2007.) $)
    hmeocna $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. K ( `' F " x ) e. J ) ) $=
      ( ctop wcel wa chomeosm co ccnv cv cima wral wi w3a cuni wf1o eqid biimpa
      ishomeo simp3d 3exp1 imp pm2.43d ) CEFZDEFZGBCDHIZFZBJAKZLCFADMZUEUFUHUHU
      JNNUEUFUHUHUJUEUFUHOZUHGCPZDPZBQZBUILDFACMZUJUKUHUNUOUJOAUGBCDULUMULRUMRT
      SUAUBUCUD $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The image of an open set by a homeomorphism is an open set.
       (Contributed by FL, 5-Mar-2007.) $)
    hmeocnb $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. J ( F " x ) e. K ) ) $=
      ( ctop wcel wa chomeosm co cv cima wral wi w3a cuni wf1o ccnv eqid biimpa
      ishomeo simp2d 3exp1 imp pm2.43d ) CEFZDEFZGBCDHIZFZBAJZKDFACLZUEUFUHUHUJ
      MMUEUFUHUHUJUEUFUHNZUHGCOZDOZBPZUJBQUIKCFADLZUKUHUNUJUONAUGBCDULUMULRUMRT
      SUAUBUCUD $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.  $d X x $.  $d Y x $.
    hmeobc.1 $e |- X = U. J $.
    hmeobc.2 $e |- Y = U. K $.
    $( F e. A is useless see ~ iscn $)
    $( A homeomorphism is a bicontinuous bijection.  (Contributed by FL,
       1-Sep-2008.) $)
    hmeobc $p |- ( ( J e. Top /\ K e. Top /\ F e. A ) -> ( F e. ( J Homeo K )
      <-> ( F : X -1-1-onto-> Y /\
          F e. ( J Cn K ) /\ `' F e. ( K Cn J ) ) ) ) $=
      ( vx ctop wcel w3a wf1o cima wral ccnv wa co adantl wi cv wf chomeosm ccn
      simp1 3ad2ant1 simp3 jca f1ocnv syl imacnvcnv eqcomi eleq1i ralbii biimpi
      f1of 3ad2ant2 3jca wrel frel wceq a1i eleq1d ralbidv biimpcd a1d imp 3imp
      com12 simpr2r impbida ishomeo wb iscn 3adant3 ancoms 3anbi23d 3bitr4d ) C
      JKZDJKZBAKZLZEFBMZBIUAZNZDKZICOZBPZWDNCKIDOZLZWCEFBUBZWIQZFEWHUBZWHPWDNZD
      KZICOZQZLZBCDUCRKWCBCDUDRKZWHDCUDRKZLWBWJWRWBWJQZWCWLWQWJWCWBWCWGWIUESXAW
      KWIWJWKWBWCWGWKWIEFBUPUFSWJWIWBWCWGWIUGSUHXAWMWPWJWMWBWCWGWMWIWCFEWHMWMEF
      BUIFEWHUPUJUFSWJWPWBWGWCWPWIWGWPWFWOICWEWNDWNWEBWDUKZULUMUNUOUQSUHURWBWRQ
      WCWGWIWRWCWBWCWLWQUESWRWGWBWCWLWQWGWLWQWGTZTWCWKWIXCWKBUSZWIXCTEFBUTXDXCW
      IWQXDWGWPXDWGTWMXDWPWGXDWOWFICXDWNWEDWNWEVAXDXBVBVCVDVESVIVFUJVGVBVHSWKWI
      WCWQWBVJURVKIABCDEFGHVLWBWSWLWTWQWCVSVTWSWLVMWAIBCDEFGHVNVOVSVTWTWQVMZWAV
      TVSXEIWHDCFEHGVNVPVOVQVR $.
      $( [1-Dec-2010] $) $( [23-Aug-2008] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The converse of a homeomorphism is a homeomorphism.  (Contributed by FL,
       5-Mar-2007.) $)
    cnvhmpha $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> `' F e. ( K Homeo J ) ) ) $=
      ( vx chomeosm co wcel ctop ccnv cvv w3a cuni wf1o cima wral eqid syl6 imp
      wa wi cnvexg hmeomap f1ocnv hmeocna hmeocnb wceq wrel f1orel dfrel2 sylib
      adantr imaeq1d eleq1d ralbidva mpbird 3jca ancoms 3adant3 ishomeo sylibrd
      cv ex 3exp impcom com3l mpcom com12 ) ABCEFZGZBHGZCHGZSZAIZCBEFGZVMJGZVIV
      LVNTAVHUAVLVOVIVNVKVJVOVIVNTZTVKVJVOVPVKVJVOKVICLZBLZVMMZVMDVAZNBGDCOZVMI
      ZVTNZCGZDBOZKZVNVKVJVIWFTZVOVJVKWGVLVIWFVLVISZVSWAWEVLVIVSVLVIVRVQAMZVSAB
      CVRVQVRPZVQPZUBZVRVQAUCQRVLVIWADABCUDRWHWEAVTNZCGZDBOZVLVIWODABCUERWHWDWN
      DBWHVTBGZSZWCWMCWQWBAVTWHWBAUFZWPVLVIWRVLVIWIWRWLWIAUGWRVRVQAUHAUIUJQRUKU
      LUMUNUOUPVBUQURDJVMCBVQVRWKWJUSUTVCVDVEVFVG $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d J f j k $.  $d K f j k $.
    $( Express the predicate ` J ` is homeomorph to ` K ` .  (Contributed by
       FL, 20-Feb-2007.) $)
    hmph $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K <->
      E. f f e. ( J Homeo K ) ) ) $=
      ( vj vk ctop wcel wa chomeo wbr cv chomeosm co wex w3a wceq opreq1 eleq2d
      eleq1 exbidv 3anbi13d df-3an bicomd opreq2 eqcoms 3anbi23d syl5rbbr brabg
      df-hmph bianabs ) BFGZCFGZHZBCIJAKZBCLMZGZANZDKZFGZEKZFGZUNURUTLMZGZANZOU
      KVAUNBUTLMZGZANZOZUMUQHZDEBCFFIURBPZUSUKVDVGVAURBFSVJVCVFAVJVBVEUNURBUTLQ
      RTUAVIUKULUQOUTCPZVHUKULUQUBVKULVAUQVGUKVKVAULUTCFSUCVKUPVFAVKUOVEUNUOVEP
      CUTCUTBLUDUERTUFUGDEAUIUHUJ $.
      $( [1-Dec-2010] $) $( [14-Feb-2007] $)
  $}

