$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Homeo $. $( Class of all homeomorphisms. $)
  $c ~= $. $( The relation "is homeomorph to." $)

  $( Extend class notation with the class of all homeomorphisms. $)
  chomeosm $a class Homeo $.

  $( Extend class notation with the relation "is homeomorph to.". $)
  chomeo $a class ~= $.

  ${
    $d j k f x $.
    $( Function returning all the homeomorphisms from topology ` j ` to
       topology ` k ` . $)
    df-homeo $a |- Homeo = ( j e. Top , k e. Top |->
            { f | ( f : U. j -1-1-onto-> U. k /\ A. x e. j ( f " x ) e. k /\
                    A. x e. k ( `' f " x ) e. j ) } ) $.
  $}

  ${
    $d x y f $.
    $( Definition of the relation ` x ` is homeomorph to ` y ` . $)
    df-hmph $a |- ~= = ( ( Top X. Top ) \ ( `' Homeo " { (/) } ) ) $.
  $}

  ${
    $d J f j k x $.  $d K f k x $.  $d X f j k $.  $d Y f k $.
    homeofval.1 $e |- X = U. J $.
    homeofval.2 $e |- Y = U. K $.
    $( The set of all the homeomorphisms between two topologies.  (Contributed
       by FL, 20-Feb-2007.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    homeofval $p |- ( ( J e. Top /\ K e. Top ) -> ( J Homeo K ) =
    { f | ( f : X -1-1-onto-> Y /\ A. x e. J ( f " x ) e. K /\
    A. x e. K ( `' f " x ) e. J ) } ) $=
      ( vj vk ctop wcel cv wf1o wral w3a cab cvv wceq cuni cima ccnv co wa cmap
      chomeosm wss wf 3ad2ant1 wb uniexg syl5eqel elmapg syl2anr syl5ibr abssdv
      f1of oprex ssexg sylancl unieq syl6eqr f1oeq2 syl raleq ralbidv 3anbi123d
      eleq2 abbidv f1oeq3 df-homeo ovmpt2g mpd3an3 ) CKLZDKLZEFBMZNZVPAMZUAZDLZ
      ACOZVPUBVRUAZCLZADOZPZBQZRLZCDUFUCWFSVNVOUDZWFFEUEUCZUGWIRLWGWHWEBWIWEVPW
      ILZWHEFVPUHZVQWAWKWDEFVPUQUIVOFRLERLWJWKUJVNVOFDTZRHDKUKULVNECTZRGCKUKULF
      EVPRRUMUNUOUPFEUEURWFWIRUSUTIJCDKKIMZTZJMZTZVPNZVSWPLZAWNOZWBWNLZAWPOZPZB
      QWFUFEWQVPNZWSACOZWCAWPOZPZBQRWNCSZXCXGBXHWRXDWTXEXBXFXHWOESWRXDUJXHWOWME
      WNCVAGVBWOEWQVPVCVDWSAWNCVEXHXAWCAWPWNCWBVHVFVGVIWPDSZXGWEBXIXDVQXEWAXFWD
      XIWQFSXDVQUJXIWQWLFWPDVAHVBWQFEVPVJVDXIWSVTACWPDVSVHVFWCAWPDVEVGVIABIJVKV
      LVM $.
      $( [15-Dec-2013] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F f x $.  $d J f x $.  $d K f x $.  $d X f $.  $d Y f $.
    ishomeo.1 $e |- X = U. J $.
    ishomeo.2 $e |- Y = U. K $.
    $( The predicate F is a homeomorphism between topology ` J ` and topology
       ` K ` .  Based on Bourbaki TG I.2.  (Contributed by FL, 20-Feb-2007.)
       (Revised by Mario Carneiro, 30-May-2014.) $)
    ishomeo $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Homeo K ) <->
      ( F : X -1-1-onto-> Y /\
        A. x e. J ( F " x ) e. K /\
        A. x e. K ( `' F " x ) e. J ) ) ) $=
      ( vf ctop wcel cv wf1o cima wral ccnv w3a cvv wi a1i cab homeofval eleq2d
      wa chomeosm elex f1of 3ad2ant1 cuni uniexg adantr syl5eqel fex expcom syl
      co wf syl5 wb wceq f1oeq1 imaeq1 eleq1d ralbidv cnveq 3anbi123d pm5.21ndd
      imaeq1d elabg bitrd ) CJKZDJKZUDZBCDUEUPZKBEFILZMZVOALZNZDKZACOZVOPZVQNZC
      KZADOZQZIUAZKZEFBMZBVQNZDKZACOZBPZVQNZCKZADOZQZVMVNWFBAICDEFGHUBUCVMBRKZW
      GWPWGWQSVMBWFUFTWPEFBUQZVMWQWHWKWRWOEFBUGUHVMERKZWRWQSVMECUIZRGVKWTRKVLCJ
      UJUKULWRWSWQEFRBUMUNUOURWQWGWPUSSVMWEWPIBRVOBUTZVPWHVTWKWDWOEFVOBVAXAVSWJ
      ACXAVRWIDVOBVQVBVCVDXAWCWNADXAWBWMCXAWAWLVQVOBVEVHVCVDVFVITVGVJ $.
      $( [30-May-2014] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    hmeomap.1 $e |- X = U. J $.
    hmeomap.2 $e |- Y = U. K $.
    $( A homeomorphism is a 1-1-onto mapping.  (Contributed by FL,
       5-Mar-2007.) $)
    hmeomap $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> F : X -1-1-onto-> Y ) ) $=
      ( vx ctop wcel wa chomeosm co wf1o cv cima wral ccnv w3a ishomeo syl6bi
      simp1 ) BIJCIJKABCLMJDEANZAHOZPCJHBQZARUDPBJHCQZSUCHABCDEFGTUCUEUFUBUA $.
      $( [30-May-2014] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The image of an open set by the converse of a homeomorphism is an open
       set.  (Contributed by FL, 5-Mar-2007.) $)
    hmeocna $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. K ( `' F " x ) e. J ) ) $=
      ( ctop wcel wa chomeosm co cuni wf1o cv cima wral ccnv eqid ishomeo simp3
      w3a syl6bi ) CEFDEFGBCDHIFCJZDJZBKZBALZMDFACNZBOUDMCFADNZSUFABCDUAUBUAPUB
      PQUCUEUFRT $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The image of an open set by a homeomorphism is an open set.
       (Contributed by FL, 5-Mar-2007.) $)
    hmeocnb $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> A. x e. J ( F " x ) e. K ) ) $=
      ( ctop wcel wa chomeosm co cuni wf1o cv cima wral ccnv eqid ishomeo simp2
      w3a syl6bi ) CEFDEFGBCDHIFCJZDJZBKZBALZMDFACNZBOUDMCFADNZSUEABCDUAUBUAPUB
      PQUCUEUFRT $.
      $( [1-Dec-2010] $) $( [5-Mar-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.  $d X x $.  $d Y x $.
    $( A homeomorphism is a bicontinuous bijection.  (Contributed by FL,
       1-Sep-2008.)  (Revised by Mario Carneiro, 30-May-2014.) $)
    hmeobc $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Homeo K ) <->
                   ( F e. ( J Cn K ) /\ `' F e. ( K Cn J ) ) ) ) $=
      ( vx ctop wcel wa cuni wf1o cima wral ccnv ccn co wf eqid iscn adantr syl
      wb w3a chomeosm f1of adantl biantrurd bitr4d ancoms f1ocnv wrel wceq frel
      dfrel2 sylib imaeq1d eleq1d ralbidv 3bitr2d anbi12d ancom syl6bb pm5.32da
      3anass syl6rbbr ishomeo wfn cnf 3expa ffn ancom1s anim12dan dff1o4 sylibr
      cv ex pm4.71rd 3bitr4d ) BEFZCEFZGZBHZCHZAIZADVMZJZCFZDBKZALZWCJBFDCKZUAZ
      WBABCMNFZWGCBMNFZGZGZABCUBNFWLVSWMWBWFWHGZGWIVSWBWLWNVSWBGZWLWHWFGWNWOWJW
      HWKWFWOWJVTWAAOZWHGZWHVSWJWQTWBDABCVTWAVTPZWAPZQRWOWPWHWBWPVSVTWAAUCUDZUE
      UFWOWKWAVTWGOZWGLZWCJZCFZDBKZGZXEWFVSWKXFTZWBVRVQXGDWGCBWAVTWSWRQUGRWOXAX
      EWOWAVTWGIZXAWBXHVSVTWAAUHUDWAVTWGUCSUEWOXDWEDBWOXCWDCWOXBAWCWOAUIZXBAUJW
      OWPXIWTVTWAAUKSAULUMUNUOUPUQURWHWFUSUTVAWBWFWHVBVCDABCVTWAWRWSVDVSWLWBVSW
      LWBVSWLGAVTVEZWGWAVEZGWBVSWJXJWKXKVSWJGWPXJVQVRWJWPABCVTWAWRWSVFVGVTWAAVH
      SVSWKGXAXKVRVQWKXAVRVQWKXAWGCBWAVTWSWRVFVGVIWAVTWGVHSVJVTWAAVKVLVNVOVP $.
      $( [30-May-2014] $) $( [23-Aug-2008] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The converse of a homeomorphism is a homeomorphism.  (Contributed by FL,
       5-Mar-2007.) $)
    cnvhmpha $p |- ( ( J e. Top /\ K e. Top ) ->
      ( F e. ( J Homeo K ) -> `' F e. ( K Homeo J ) ) ) $=
      ( ctop wcel wa ccn co ccnv chomeosm wrel wceq w3a cuni wf eqid cnf hmeobc
      frel syl 3expa dfrel2 sylib simpr eqeltrd ancld expimpd wb ancoms 3imtr4d
      a1d ) BDEZCDEZFZABCGHZEZAIZCBGHEZFURUQIZUOEZFZABCJHEUQCBJHEZUNUPURVAUNUPF
      ZURUTVCUTURVCUSAUOVCAKZUSALULUMUPVDULUMUPMBNZCNZAOVDABCVEVFVEPVFPQVEVFAST
      UAAUBUCUNUPUDUEUKUFUGABCRUMULVBVAUHUQCBRUIUJ $.
      $( [30-May-2014] $) $( [5-Mar-2007] $)
  $}

  ${
    $d J f $.  $d K f $.  $d f x y z $.
    $( Express the predicate ` J ` is homeomorph to ` K ` .  (Contributed by
       FL, 20-Feb-2007.)  (Revised by Mario Carneiro, 30-May-2014.) $)
    hmph $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K <->
                 ( J Homeo K ) =/= (/) ) ) $=
      ( vx vy vf vz chomeo wbr ctop chomeosm ccnv c0 cima wa co wb df-br sylibr
      wcel cv cxp csn wn wne cdif df-hmph breqi brdif bitri cop opelxpi syl cfv
      ibar wceq wfn cvv cuni wf1o wral w3a cab df-homeo cmap oprex wf vex uniex
      f1of elmap 3ad2ant1 abssi ssexi fnmpt2i fniniseg mp2an baib df-opr eqeq1i
      0ex 3bitr4g necon3bbid bitr3d syl5bb ) ABGHZABIIUAZHZABJKLUBMZHZUCZNZAISB
      ISNZABJOZLUDZWEABWFWHUEZHWKABGWOUFUGABWFWHUHUIWLWJWKWNWLWGWJWKPWLABUJZWFS
      ZWGABIIUKZABWFQRWGWJUNULWLWIWMLWLWPWHSZWPJUMZLUOZWIWMLUOWLWQWSXAPWRWSWQXA
      JWFUPLUQSWSWQXANPCDIICTZURZDTZURZETZUSZXFFTZMXDSFXBUTZXFKXHMXBSFXDUTZVAZE
      VBZJFECDVCXLXEXCVDOZXEXCVDVEXKEXMXGXIXFXMSZXJXGXCXEXFVFXNXCXEXFVIXEXCXFXD
      DVGVHXBCVGVHVJRVKVLVMVNVTWFLWPJUQVOVPVQULABWHQWMWTLABJVRVSWAWBWCWD $.
      $( [30-May-2014] $) $( [14-Feb-2007] $)
  $}

