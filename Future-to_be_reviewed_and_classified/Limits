$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c fLim $.

  $( Extend class notation with a function returning the limit of a filter. $)
  cflim $a class fLim $.

  ${
    $d x a f l $.
    $( Define a function (indexed by a topology ` x ` ) whose value is the
       limits of a filter ` a ` . $)
    df-flim $a |- fLim = ( x e. Top |->
                    ( a e. { f e. Fil | U. f = U. x } |->
                     { l e. U. x | ( ( nei ` x ) ` { l } ) C_ a } ) ) $.
  $}

  ${
    $d J a f l x $.  $d X a f l x $.
    flimfval.1 $e |- X = U. J $.
    $( Functions whose values are the limits of the filters.  (Contributed by
       FL, 1-Sep-2008.)  (Revised by Mario Carneiro, 29-Nov-2013.) $)
    flimfval $p |- ( J e. Top -> ( fLim ` J ) =
                   ( a e. { f e. Fil | U. f = X } |->
                     { l e. X | ( ( nei ` J ) ` { l } ) C_ a } ) ) $=
      ( vx ctop wcel cv cuni wceq cfil crab cnei cfv wss cmpt cvv cflim csn cpw
      wi wral eqimss visset elpw sspwuni bitri sylibr rgen rabss mpbir syl5eqel
      a1i uniexg pwexg 3syl ssexg sylancr mptexg syl unieq syl6eqr eqeq2d fveq2
      rabbidv fveq1d sseq1d rabeqbidv mpteq12dv df-flim fvmptg mpdan ) BHIZDAJZ
      KZCLZAMNZEJUAZBOPZPZDJZQZECNZRZSIZBTPWFLVOVSSIZWGVOVSCUBZUBZQZWJSIZWHWKVR
      VPWJIZUCZAMUDWNAMWNVPMIVRVQCQZWMVQCUEWMVPWIQWOVPWIAUFUGVPCUHUIUJUOUKVRAMW
      JULUMVOCSIWISIWLVOCBKZSFBHUPUNCSUQWISUQURVSWJSUSUTDVSWESVAVBGBDVQGJZKZLZA
      MNZVTWQOPZPZWCQZEWRNZRWFHSTWQBLZDWTXDVSWEXEWSVRAMXEWRCVQXEWRWPCWQBVCFVDZV
      EVGXEXCWDEWRCXFXEXBWBWCXEVTXAWAWQBOVFVHVIVJVKGADEVLVMVN $.
      $( [29-Nov-2013] $) $( [17-Aug-2008] $)
  $}

  ${
    $d a f l F $.  $d a f l J $.  $d a f l X $.  $d f Y $.
    flimval.1 $e |- X = U. J $.
    flimval.2 $e |- Y = U. F $.
    $( The set of limit points of a filter.  (Contributed by Jeff Hankins,
       4-Sep-2009.) $)
    flimval $p |- ( ( J e. Top /\ F e. Fil /\ X = Y ) -> ( ( fLim ` J ) ` F ) =
    { l e. X | ( ( nei ` J ) ` { l } ) C_ F } ) $=
      ( va vf ctop wcel cfil wceq cfv cv wss crab wa cuni cvv csn cnei flimfval
      cflim cmpt fveq1d adantr eqcom unieq syl6eqr eqeq1d elrab biimpri sylan2b
      uniexg syl5eqel rabexg syl sseq2 rabbidv eqid fvmptg syl2anc eqtrd 3impb
      ) BJKZALKZCDMZABUDNZNZEOUABUBNNZAPZECQZMVFVGVHRZRVJAHIOZSZCMZILQZVKHOZPZE
      CQZUEZNZVMVFVJWCMVNVFAVIWBIBCHEFUCUFUGVNAVRKZVMTKZWCVMMVFVHVGDCMZWDCDUHWD
      VGWFRVQWFIALVOAMZVPDCWGVPASDVOAUIGUJUKULUMUNVFCTKWEVFCBSTFBJUOUPVLECTUQUR
      HAWAVMVRTWBVSAMVTVLECVSAVKUSUTWBVAVBVCVDVE $.
      $( [29-Nov-2013] $) $( [4-Sep-2009] $)
  $}

  ${
    $d l A $.  $d l F $.  $d l J $.  $d l X $.  $d l Y $.
    elflim.1 $e |- X = U. J $.
    elflim.2 $e |- Y = U. F $.
    $( The predicate "is a limit point of a filter."  (Contributed by Jeff
       Hankins, 4-Sep-2009.) $)
    elflim $p |- ( ( J e. Top /\ F e. Fil /\ X = Y ) ->
                   ( A e. ( ( fLim ` J ) ` F ) <->
                     ( A e. X /\ ( ( nei ` J ) ` { A } ) C_ F ) ) ) $=
      ( vl ctop wcel cfil wceq w3a cflim cfv cv csn cnei wss crab eleq2d fveq2d
      wa flimval sneq sseq1d elrab syl6bb ) CIJBKJDELMZABCNOOZJAHPZQZCROZOZBSZH
      DTZJADJAQZUMOZBSZUCUIUJUPABCDEHFGUDUAUOUSHADUKALZUNURBUTULUQUMUKAUEUBUFUG
      UH $.
      $( [29-Nov-2013] $) $( [4-Sep-2009] $)
  $}

  ${
    limfilss.1 $e |- X = U. J $.
    limfilss.2 $e |- Y = U. F $.
    limfilss.3 $e |- Z = U. G $.
    $( A limit point of a filter is a limit point of a finer filter.
       (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    limfilss $p |- ( ( ( ( J e. Top /\ F e. Fil /\ G e. Fil ) /\ X = Y /\ X = Z
    ) /\ F C_ G /\ A e. ( ( fLim ` J ) ` F ) ) -> A e. ( ( fLim ` J ) ` G ) )
        $=
      ( wcel cfil w3a wceq wss cfv wa wb elflim 3expa ctop cflim csn cnei sstr2
      wi anim2d adantl 3adantl3 3adant3 adantr 3adantl2 3adant2 3imtr4d 3impia
      com12 ) DUAKZBLKZCLKZMZEFNZEGNZMZBCOZABDUBPZPKZACVEPKZVCVDQAEKZAUCDUDPPZB
      OZQZVHVICOZQZVFVGVDVKVMUFVCVDVJVLVHVJVDVLVIBCUEUPUGUHVCVFVKRZVDUTVAVNVBUQ
      URVAVNUSUQURVAVNABDEFHISTUIUJUKVCVGVMRZVDUTVBVOVAUQUSVBVOURUQUSVBVOACDEGH
      JSTULUMUKUNUO $.
      $( [5-Sep-2009] $)
  $}

  ${
    flimelbas.1 $e |- X = U. J $.
    flimelbas.2 $e |- Y = U. F $.
    $( A limit point of a filter belongs to its base set.  (Contributed by Jeff
       Hankins, 4-Sep-2009.) $)
    flimelbas $p |- ( ( ( J e. Top /\ F e. Fil /\ X = Y ) /\ A e. ( ( fLim ` J
    ) ` F ) ) -> A e. X ) $=
      ( ctop wcel cfil wceq w3a cflim cfv csn cnei wss elflim simprbda ) CHIBJI
      DEKLABCMNNIADIAOCPNNBQABCDEFGRS $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    flimnei.1 $e |- X = U. J $.
    flimnei.2 $e |- Y = U. F $.
    $( A filter contains all of the neighborhoods of its limit points.
       (Contributed by Jeff Hankins, 4-Sep-2009.) $)
    flimnei $p |- ( ( ( J e. Top /\ F e. Fil /\ X = Y ) /\ A e. ( ( fLim ` J )
    ` F ) /\ N e. ( ( nei ` J ) ` { A } ) ) -> N e. F ) $=
      ( ctop wcel cfil wceq w3a cflim cfv csn cnei wa wss elflim simplbda sseld
      3impia ) CIJBKJEFLMZABCNOOJZDAPCQOOZJDBJUDUERUFBDUDUEAEJUFBSABCEFGHTUAUBU
      C $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    $d J x y $.  $d S x y $.  $d X x y $.
    neifil.1 $e |- X = U. J $.
    $( The neighborhoods of a non empty set is a filter.  Bourbaki TG I.36,
       example 2.  (Contributed by FL, 19-Sep-2007.) $)
    neifil $p |- ( ( J e. Top /\ S C_ X /\ S =/= (/) ) ->
      ( ( nei ` J ) ` S ) e. Fil ) $=
      ( vy vx ctop wcel wss c0 wne w3a cnei cfv wa cv wi wral biimpa 3adant3 wn
      cuni wrex cpw cin cfil 0nnei 3adant2 unnei tpnei eqeltrd jca elpwi sseq2d
      sylan2 ssnei2 exp43 rexlimdv imp adantlr syldan ralrimiva 3expib 3ad2ant1
      com23 innei ralrimivv 3jca eqid isfil2 sylibr ) BGHZACIZAJKZLZJABMNNZHUAZ
      VPUBZVPHZOZEPZFPZIZEVPUCZWBVPHZQZFVRUDZRZWBWAUEVPHZEVPRFVPRZLVPUFHVOVTWHW
      JVOVQVSVLVNVQVMABUGUHVLVMVSVNVLVMOZVRCVPABCDUIZVLVMCVPHABCDUJSUKTULVLVMWH
      VNWKWFFWGWKWBWGHZWBCIZWFWMWKWBVRIZWNWBVRUMWKWOWNWKVRCWBWLUNSUOVLWNWFVMVLW
      NWFVLWDWNWEVLWCWNWEQEVPVLWAVPHZWCWNWEABWBWACDUPUQURVEUSUTVAVBTVOWIFEVPVPV
      LVMWEWPOWIQVNVLWEWPWIABWAWBVFVCVDVGVHFEVPVRVRVIVJVK $.
      $( [28-Nov-2013] $) $( [18-Sep-2007] $)
  $}

  ${
    $d f u v w x y z J $.  $d f u v w x y z X $.
    hausflim.1 $e |- X = U. J $.
    $( A condition for a topology to be Hausdorff in terms of filters.  A
       topology is Hausdorff iff every filter has at most one limit point.
       (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    hausflim $p |- ( J e. Top -> ( J e. Haus
        <-> A. f e. Fil ( X = U. f -> E* x x e. ( ( fLim ` J ) ` f ) ) ) ) $=
      ( vu vv vy wcel wne c0 wceq cfv wi wral cfil wa syl wn wss vz vw ctop cha
      cv cin csn cnei wrex cuni cflim wmo hausnei2 wal w3a flimelbas ex anim12d
      eqid imp neeq1 sneq fveq2d rexeqdv imbi12d neeq2 rexbidv rcla42v 3ad2ant2
      filesn adantr df-ne pm2.27 simp3 simpll2 flimnei adantrr adantlrr adantrl
      3expa adantlrl syl111anc 3adant3 eqeltrrd 3exp com3l rexlimivv syl6 com13
      filint impr syl5bir mt3d expr com23 alrimdv 3expib ralrimdv ralnex bicomi
      syld exp4a ralbii bitr3i wex cun cfi cfg cfbas cvv wb fvex unex a1i snssi
      fsubbas 3ad2ant1 visset snnz neifil mp3an3 sylan2 filfbas mpbir2and unnei
      ax-mp elflim ad2antrl sstri sstrd exp520 com24 3impib impcom eleq1 anbi1d
      notbid anbi12d eleq2d bitri ssun1 tpnei mpbid ne0i sylancr simpr sylan2br
      ssn0 2ralbii jca fbunfip mpbird fgfil uneq12d unidm syl6req uniun syl5eqr
      fiuni fgbas eqtrd adantl simpll ssfii fbssfg imp3a simplr biimpi 3ad2ant3
      ssun2 ad2antlr eqeq1 anbi2d eqeq2 sylan9bb cla42ev syl21anc unieq 2exbidv
      jcai eqeq2d fveq2 rcla4ev syldan expcom exanali exbii exnal anbi2i rexbii
      mpdd annim rexnal syl6ib syl5bi con4d ralrimdvv impbid mo4 imbi2i syl6bbr
      bitrd ) CUCIZCUDIUAUEZUBUEZJZFUEZGUEZUFZKLZGUXEUGZCUHMZMZUIZFUXDUGZUXLMZU
      IZNZUBDOUADOZDBUEZUJZLZAUEZUXTCUKMZMZIZAULZNZBPOZUAUBGFCDEUMUXCUXSUYBUYFH
      UEZUYEIZQZUYCUYJLZNZHUNZAUNZNZBPOZUYIUXCUXSUYRUXCUXSUYQBPUXCUXSUXTPIZUYBU
      YPUXCUYSUYBQUXSUYPUXCUYSUYBUXSUYPNUXCUYSUYBUOZUXSUYOAUYTUXSUYNHUYTUYLUXSU
      YMUYTUYLUXSUYMNUYTUYLQZUXSUYCUYJJZUXJGUYJUGZUXLMZUIZFUYCUGZUXLMZUIZNZUYMV
      UAUYCDIZUYJDIZQZUXSVUINUYTUYLVULUYTUYFVUJUYKVUKUYTUYFVUJUYCUXTCDUYAEUYAUS
      ZUPUQUYTUYKVUKUYJUXTCDUYAEVUMUPUQURUTUXRVUIUYCUXEJZUXNFVUGUIZNUAUBUYCUYJD
      DUXDUYCLZUXFVUNUXQVUOUXDUYCUXEVAVUPUXNFUXPVUGVUPUXOVUFUXLUXDUYCVBVCVDVEUX
      EUYJLZVUNVUBVUOVUHUXEUYJUYCVFVUQUXNVUEFVUGVUQUXJGUXMVUDVUQUXKVUCUXLUXEUYJ
      VBVCVDVGVEVHRUYTUYLVUIUYMUYTUYLVUIQZQZUYMKUXTIZUYTVUTSZVURUYSUXCVVAUYBUXT
      VJVIVKUYMSZVUBVUSVUTUYCUYJVLUYTUYLVUIVUBVUTNVUBVUIVUAVUTVUBVUIVUHVUAVUTNZ
      VUBVUHVMUXJVVCFGVUGVUDVUAUXGVUGIZUXHVUDIZQZUXJVUTVUAVVFUXJVUTVUAVVFUXJUOU
      XIKUXTVUAVVFUXJVNVUAVVFUXIUXTIZUXJVUAVVFQUYSUXGUXTIZUXHUXTIZVVGUXCUYSUYBU
      YLVVFVOUYTUYFVVFVVHUYKUYTUYFQVVDVVHVVEUYTUYFVVDVVHUYCUXTCUXGDUYAEVUMVPVTV
      QVRUYTUYKVVFVVIUYFUYTUYKQVVEVVIVVDUYTUYKVVEVVIUYJUXTCUXHDUYAEVUMVPVTVSWAU
      XGUXHUXTWJWBWCWDWEWFWGWHWIWKWLWMWNXAUQWOWPWPWQWOXBWRUXCUYRUXRUAUBDDUXCUXF
      UXDDIZUXEDIZQZUYRUXQUXCVVLUXFUYRUXQNZUXCVVLUXFVVMUXCVVLUXFUOZUXQUYRUXQSZU
      XJSZGUXMOZFUXPOZVVNUYRSZVVOUXNSZFUXPOVVRUXNFUXPWSVVTVVQFUXPVVQVVTUXJGUXMW
      SWTXCXDVVNVVRUYBUYLVVBQZHXEZAXEZQZBPUIZVVSVVNVVRUXPUXMXFZXGMZXHMZPIZVWEVV
      NVVRVWIVVNVVRQZVWGXIIZVWIVWJVWKVWFKJZKVWGISZVWJVWFXJIZVWKVWLVWMQXKVWNVWJU
      XPUXMUXOUXLXLUXKUXLXLXMZXNVWFXJXPRVWJUXPVWFTUXPKJZVWLUXPUXMUUAZVVNVWPVVRV
      VNDUXPIZVWPVVNUXODTZVWRVVLUXCVWSUXFVVJVWSVVKUXDDXOZVKVIUXCVVLVWSVWRXKUXFU
      XOCDEUUBXQUUCUXPDUUDRVKUXPVWFUUHUUEVWJVWMUXIKJZGUXMOFUXPOZVVRVVNVXBVXBVXA
      VVPFGUXPUXMUXIKVLUUIVVNVXBUUFUUGVVNVWMVXBXKZVVRVVNUXPXIIZUXMXIIZQZVXCUXCV
      VLVXFUXFUXCVVLQZVXDVXEVXGUXPPIZVXDUXCVVJVXHVVKVVJUXCVWSVXHVWTUXCVWSUXOKJV
      XHUXDUAXRZXSUXOCDEXTYAYBVQUXPYCRUXCVVKVXEVVJUXCVVKQUXMPIZVXEVVKUXCUXKDTZV
      XJUXEDXOZUXCVXKUXKKJVXJUXEUBXRZXSUXKCDEXTYAYBUXMYCRVSUUJWCFGUXPUXMUUKRVKU
      ULYDZVWGUUMRUQVVNVVRVWIVWENVWIVWJVWEVWIVWJDVWHUJZLZUYCVWHUYDMZIZUYJVXQIZQ
      ZVVBQZHXEAXEZQZVWEVWIVWJQZVXPVYBVWJVXPVWIVWJDUXPUJZUXMUJZXFZVXOVVNDVYGLZV
      VRUXCVVLVYHUXFVXGVYGDDXFDVXGVYEDVYFDUXCVVJVYEDLZVVKVVJUXCVWSVYIVWTUXOCDEY
      EYBVQUXCVVKVYFDLZVVJVVKUXCVXKVYJVXLUXKCDEYEYBVSUUNDUUOUUPWCVKVWJVWKVYGVXO
      LVXNVWGVYGVWNVYGVWGUJZLVWOVWNVYGVWFUJVYKUXPUXMUUQVWFXJUUSUURYFUUTRUVAUVBV
      YDVXPVYBVYDVXPQUXDVXQIZUXEVXQIZUXDUXELZSZVYBVYDVXPVYLVWJVWIVXPVYLNZVVNVVR
      VWIVYPNVVNVXPVWIVVRVYLVVNVWIVXPVVRVYLNZUXCVVLUXFVWIVXPVYQNNUXCVXPVWIVVLUX
      FQZVYQUXCVWIVXPVYRVYQNUXCVWIVXPVYRVVRVYLUXCVWIVXPUOZVYRVVRQZQZVYLVVJUXPVW
      HTZVYSVYLVVJWUBQXKVYTUXDVWHCDVXOEVXOUSZYGVKVYRVVJVYSVVRVVJVVKUXFUVCYHWUAU
      XPVWGVWHUXPVWGTWUAUXPVWFVWGVWQVWNVWFVWGTVWOVWFXJUVDYFZYIXNVYSVYTVWGVWHTZU
      XCVWIVYTWUENVXPUXCVYRVVRWUEUXCVVLUXFVVRWUENVVNVVRWUEVWJVWKWUEVXNVWGUVERUQ
      WQUVFXQUTZYJYDYKWOYLYMWOYLUTYNUTVYDVXPVYMVWJVWIVXPVYMNZVVNVVRVWIWUGNVVNVX
      PVWIVVRVYMVVNVWIVXPVVRVYMNZUXCVVLUXFVWIVXPWUHNNUXCVXPVWIVYRWUHUXCVWIVXPVY
      RWUHNUXCVWIVXPVYRVVRVYMWUAVYMVVKUXMVWHTZVYSVYMVVKWUIQXKVYTUXEVWHCDVXOEWUC
      YGVKVYRVVKVYSVVRVVJVVKUXFUVGYHWUAUXMVWGVWHUXMVWGTWUAUXMVWFVWGUXMUXPUVJWUD
      YIXNWUFYJYDYKWOYLYMWOYLUTYNUTVWJVYOVWIVXPVVNVYOVVRUXFUXCVYOVVLUXFVYOUXDUX
      EVLUVHUVIVKUVKVYAVYLVYMQZVYOQZAHUXDUXEVXIVXMUYCUXDLZVYAVYLVXSQZUXDUYJLZSZ
      QUYJUXELZWUKWULVXTWUMVVBWUOWULVXRVYLVXSUYCUXDVXQYOYPWULUYMWUNUYCUXDUYJUVL
      YQYRWUPWUMWUJWUOVYOWUPVXSVYMVYLUYJUXEVXQYOUVMWUPWUNVYNUYJUXEUXDUVNYQYRUVO
      UVPUVQUQUVTVWDVYCBVWHPUXTVWHLZUYBVXPVWCVYBWUQUYAVXODUXTVWHUVRUWAWUQVWAVYA
      AHWUQUYLVXTVVBWUQUYFVXRUYKVXSWUQUYEVXQUYCUXTVWHUYDUWBZYSWUQUYEVXQUYJWURYS
      YRYPUVSYRUWCUWDUWEUQUWKVWEUYQSZBPUIVVSVWDWUSBPVWDUYBUYPSZQWUSVWCWUTUYBVWC
      UYOSZAXEWUTVWBWVAAUYLUYMHUWFUWGUYOAUWHYTUWIUYBUYPUWLYTUWJUYQBPUWMYTUWNUWO
      UWPWEWOYLUWQUWRUYHUYQBPUYGUYPUYBUYFUYKAHUYCUYJUYEYOUWSUWTXCUXAUXB $.
      $( [28-Nov-2013] $) $( [5-Sep-2009] $)
  $}


  $c FilMap $.
  $( Extend class definition to include the neighborhood filter mapping
     function. $)
  cfm $a class FilMap $.

  $c fLimf $.
  $( Extend class definition to include the function for filter-based function
     limits. $)
  cflf $a class fLimf $.

  ${
    $d f t x y z $.
    $( Define a function that takes a filter to a neighborhood filter of the
       range.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    df-fm $a |- FilMap = ( x e. _V , y e. fBas |->
                     ( f e. ( x ^m U. y ) |-> ( filGen `
                       ( { z | E. t e. y z = ( f " t ) } u. { x } ) ) ) ) $.

    $( Define a function that gives the limits of a function ` f ` in the
       filter sense.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    df-flf $a |- fLimf = ( x e. Top , y e. Fil |-> ( f e. ( U. x ^m U. y ) |->
                     ( ( fLim ` x ) ` ( ( U. x FilMap y ) ` f ) ) ) ) $.
  $}

  ${
    $d f x y F $.  $d f w x y B $.  $d f w x y z X $.  $d f w y Y $.
    fmfval.1 $e |- Y = U. B $.
    $( Given a function from a filtered set to a topology, return the filter of
       supersets of images of filter elements.  (Contributed by Jeff Hankins,
       5-Sep-2009.)  (Revised by Mario Carneiro, 15-Dec-2013.) $)
    fmfval $p |- ( ( X e. V /\ B e. fBas ) -> ( X FilMap B ) =
                       ( f e. ( X ^m Y ) |-> ( filGen `
                         ( { x | E. y e. B x = ( f " y ) } u. { X } ) ) ) ) $=
      ( vz vw wcel cvv co cmap cv wceq cun cfg cfv cmpt cfbas cfm cima wrex cab
      csn elisset cuni opreq1 sneq uneq2d mpteq12dv unieq syl6eqr opreq2d rexeq
      fveq2d abbidv uneq1d df-fm oprex mptexg ax-mp ovmpt2 sylan ) FEKFLKCUAKFC
      UBMDFGNMZAODOBOUCPZBCUDZAUEZFUFZQZRSZTZPFEUGIJFCLUADIOZJOZUHZNMZVGBVOUDZA
      UEZVNUFZQZRSZTVMUBDFVPNMZVSVJQZRSZTVNFPZDVQWBWCWEVNFVPNUIWFWAWDRWFVTVJVSV
      NFUJUKUQULVOCPZDWCWEVFVLWGVPGFNWGVPCUHGVOCUMHUNUOWGWDVKRWGVSVIVJWGVRVHAVG
      BVOCUPURUSUQULIJABDUTVFLKVMLKFGNVADVFVLLVBVCVDVE $.
      $( [15-Dec-2013] $) $( [5-Sep-2009] $)

    $( Introduce a function that takes a function from a filtered domain to a
       set and produces a filter which consists of supersets of images of
       filter elements.  The functions which are dealt with by this function
       are similar to nets in topology.  For example, suppose we have a
       sequence filtered by the filter generated by its tails under the usual
       natural number ordering.  Then the elements of this filter are precisely
       the supersets of tails of this sequence.  Under this definition, it is
       not too difficult to see that the limit of a function in the filter
       sense captures the notion of convergence of a sequence.  As a result,
       the notion of a filter generalizes many ideas associated with sequences,
       and this function is one way to make that relationship precise in
       Metamath.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    fmval $p |- ( ( X e. A /\ B e. fBas /\ F : Y --> X ) ->
                      ( ( X FilMap B ) ` F ) = ( filGen `
                        ( { x | E. y e. B x = ( F " y ) } u. { X } ) ) ) $=
      ( vf wcel cfbas co cfv cv cima wceq wrex cab cfg cvv wf cfm csn cun wa wb
      cmap cuni uniexg syl5eqel elmapg sylan2 biimpar cmpt fmfval fveq1d imaeq1
      eqeq2d rexbidv abbidv uneq1d fveq2d eqid fvex fvmpt sylan9eq syldan 3impa
      ) FCJZDKJZGFEUAZEFDUBLZMZANZEBNZOZPZBDQZARZFUCZUDZSMZPZVIVJUEZVKEFGUGLZJZ
      WCWDWFVKVJVIGTJWFVKUFVJGDUHTHDKUIUJFGECTUKULUMWDWFVMEIWEVNINZVOOZPZBDQZAR
      ZVTUDZSMZUNZMWBWDEVLWNABDICFGHUOUPIEWMWBWEWNWGEPZWLWASWOWKVSVTWOWJVRAWOWI
      VQBDWOWHVPVNWGEVOUQURUSUTVAVBWNVCWASVDVEVFVGVH $.
      $( [15-Dec-2013] $) $( [5-Sep-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z F $.  $d x y z X $.
    $d x y z Y $.  $d x y z Z $.
    fmss.1 $e |- Y = U. B $.
    fmss.2 $e |- Z = U. C $.
    $( A finer filter produces a finer image filter.  (Contributed by Jeff
       Hankins, 16-Nov-2009.) $)
    fmss $p |- ( ( ( X e. A /\ B e. fBas /\ C e. fBas ) /\ ( F : Y --> X /\
    Y = Z /\ B C_ C ) ) -> ( ( X FilMap B ) ` F ) C_ ( ( X FilMap C ) ` F ) )
        $=
      ( vx vy vz wcel cfbas wceq wss wrex cfv eqid adantl w3a wf wa cv cima cab
      csn cun cfg cfm co cuni wfn simp2 3ad2ant1 filrn syl2an wral visset eqeq1
      ffn rexbidv elab wi crn imassrn a1i frn sstrd sseq1 syl5ibrcom a1d syl5bi
      rexlimdv ralrimiv unissb sylibr extbas1 simp3 fneq2 biimpac sylan 3adant3
      syl11anc wal ssrexv 3ad2ant3 alrimiv ss2ab unss1 syl fgss syl111anc fmval
      3expa 3ad2antr1 3adantl3 simp1 adantr feq2 3sstr4d ) EAMZBNMZCNMZUAZFEDUB
      ZFGOZBCPZUAZUCZJUDZDKUDZUEZOZKBQZJUFZEUGZUHZUIRZXNKCQZJUFZXQUHZUIRZDEBUJU
      KRZDECUJUKRZXJXRNMZYBNMZXRYBPZXSYCPXJXPNMZXPULZEPZYFXEXCDFUMZYIXIXBXCXDUN
      XFXGYLXHFEDVAZUOKJBXPDFHXPSUPUQXIYKXEXFXGYKXHXFLUDZEPZLXPURYKXFYOLXPYNXPM
      YNXMOZKBQZXFYOXOYQJYNLUSZXKYNOZXNYPKBXKYNXMUTZVBVCXFYPYOKBXFYPYOVDZXLBMXF
      YOYPXMEPXFXMDVEZEXMUUBPXFDXLVFVGFEDVHVIYNXMEVJVKZVLVNVMVOLXPEVPVQUOTEXPYJ
      YJSVRWDXJYANMZYAULZEPZYGXEXDDGUMZUUDXIXBXCXDVSZXFXGUUGXHXFYLXGUUGYMXGYLUU
      GFGDVTWAWBWCKJCYADGIYASUPUQXIUUFXEXFXGUUFXHXFYOLYAURUUFXFYOLYAYNYAMYPKCQZ
      XFYOXTUUIJYNYRYSXNYPKCYTVBVCXFYPYOKCXFUUAXLCMUUCVLVNVMVOLYAEVPVQUOTEYAUUE
      UUESVRWDXJXPYAPZYHXJXOXTVDZJWEUUJXJUUKJXIUUKXEXHXFUUKXGXNKBCWFWGTWHXOXTJW
      IVQXPYAXQWJWKXRYBWLWMXBXCXIYDXSOZXDXBXCUCXGXFUULXHXBXCXFUULJKABDEFHWNWOWP
      WQXJXBXDGEDUBZYEYCOXEXBXIXBXCXDWRWSXEXDXIUUHWSXIUUMXEXFXGUUMXHXGXFUUMFGED
      WTWAWCTJKACDEGIWNWMXA $.
      $( [16-Nov-2009] $)
  $}

  ${
    $d t w A $.  $d t w B $.  $d t w F $.  $d t w X $.  $d t w Y $.
    fmfil.1 $e |- Y = U. B $.
    $( A mapping filter is a filter.  (Contributed by Jeff Hankins,
       18-Sep-2009.) $)
    fmfil $p |- ( ( X e. A /\ B e. fBas /\ F : Y --> X ) -> ( ( X FilMap B ) `
        F
    ) e. Fil ) $=
      ( vw vt wcel cfbas cfv cv wceq cfil wss eqid cvv wral syl 3ad2ant3 wf w3a
      cfm co cima wrex cab csn cun cfg fmval cuni wfn filrn sylan2 3adant1 ciun
      ffn wfun visset funimaex a1d ralrimiv dfiun2g crn imassrn a1i sstrd iunss
      ffun frn sylibr eqsstr3d extbas1 syl11anc fgfil eqeltrd ) DAIZBJIZEDCUAZU
      BZCDBUCUDKGLCHLZUEZMHBUFGUGZDUHUIZUJKZNGHABCDEFUKWAWEJIZWFNIWAWDJIZWDULZD
      OWGVSVTWHVRVTVSCEUMWHEDCURHGBWDCEFWDPUNUOUPWAWIHBWCUQZDWAWCQIZHBRWJWIMWAW
      KHBWAWKWBBIZVTVRWKVSVTCUSWKEDCVJCWBHUTVASTVBVCHGBWCQVDSWAWCDOZHBRZWJDOVTV
      RWNVSVTWMHBVTWMWLVTWCCVEZDWCWOOVTCWBVFVGEDCVKVHVBVCTHBWCDVIVLVMDWDWIWIPVN
      VOWEVPSVQ $.
      $( [1-Dec-2010] $) $( [18-Sep-2009] $)
  $}

  ${
    $d t w A $.  $d t w B $.  $d t w F $.  $d t w X $.  $d t w Y $.
    fmbas.1 $e |- Y = U. B $.
    $( The base set of a mapping filter is the first argument.  (Contributed by
       Jeff Hankins, 18-Sep-2009.) $)
    fmbas $p |- ( ( X e. A /\ B e. fBas /\ F : Y --> X ) -> U. ( ( X FilMap B )
    ` F ) = X ) $=
      ( vw vt wcel cfbas cfv cuni cv wceq wss eqid cvv wral syl a1d wf w3a cima
      cfm co wrex cab csn cun cfg fmval unieqd wfn ffn sylan2 3adant1 ciun wfun
      filrn visset funimaex ralrimiv 3ad2ant3 dfiun2g crn imassrn a1i frn sstrd
      ffun iunss sylibr eqsstr3d extbas1 syl11anc fgbas simp1 extbas2 3eqtr2d )
      DAIZBJIZEDCUAZUBZCDBUDUEKZLGMCHMZUCZNHBUFGUGZDUHUIZUJKZLZWHLZDWCWDWIGHABC
      DEFUKULWCWHJIZWKWJNWCWGJIZWGLZDOZWLWAWBWMVTWBWACEUMWMEDCUNHGBWGCEFWGPUSUO
      UPWCWNHBWFUQZDWCWFQIZHBRZWPWNNWBVTWRWAWBWQHBWBWQWEBIZWBCURWQEDCVJCWEHUTVA
      STVBVCHGBWFQVDSWCWFDOZHBRZWPDOWBVTXAWAWBWTHBWBWTWSWBWFCVEZDWFXBOWBCWEVFVG
      EDCVHVITVBVCHBWFDVKVLVMZDWGWNWNPZVNVOWHWKWKPVPSWCWOVTWKDNXCVTWAWBVQDAWGWN
      XDVRVOVS $.
      $( [1-Dec-2010] $) $( [18-Sep-2009] $)
  $}


  ${
    $d t w x y z A $.  $d t w x y z B $.  $d t w x y C $.  $d t w x y z F $.
    $d x y z L $.  $d t w x y z X $.  $d t w x y z Y $.
    elfm.1 $e |- Y = U. B $.
    $( An element of a mapping filter.  (Contributed by Jeff Hankins,
       8-Sep-2009.) $)
    elfm $p |- ( ( X e. C /\ B e. fBas /\ F : Y --> X ) -> ( A e. ( ( X
    FilMap B ) ` F ) <-> ( A C_ X /\ E. x e. B ( F " x ) C_ A ) ) ) $=
      ( vw vt vy wcel cv wceq wrex wss wa 3ad2ant3 syl11anc wi cfbas wf w3a cfm
      co cfv cima cab csn cun cfg cuni fmval eleq2d wfn ffn eqid sylan2 3adant1
      wb filrn ciun cvv wral wfun ffun visset funimaex syl a1d ralrimiv dfiun2g
      crn imassrn a1i frn sstrd iunss sylibr eqsstr3d extbas1 elfg simp1 sseq2d
      extbas2 wo elun eqeq1 rexbidv elab elsn orbi12i bitri simp2 adantr biimpa
      sseq1 imaeq2 sseq1d rcla4ev 3exp1 rexlimdv wex c0 wne fbasne0 sylib sstr2
      n0 3ad2ant2 sylbid exlimdv 3imp syl3an2 jaod syl5bi ssun1 eqeq2d ad2antrl
      imp mpan2 elabg 3syl mpbird sseldd simprr exp32 impbid anbi12d 3bitrd ) F
      DLZCUALZGFEUBZUCZBEFCUDUEUFZLBIMZEJMZUGZNZJCOZIUHZFUIZUJZUKUFZLZBUUCULZPZ
      KMZBPZKUUCOZQZBFPZEAMZUGZBPZACOZQYNYOUUDBIJDCEFGHUMUNYNUUCUALZUUEUUKUTYNU
      UAUALZUUAULZFPZUUQYLYMUURYKYMYLEGUOUURGFEUPJICUUAEGHUUAUQVAURUSYNUUSJCYRV
      BZFYNYRVCLZJCVDUVAUUSNYNUVBJCYNUVBYQCLZYMYKUVBYLYMEVEZUVBGFEVFZEYQJVGVHVI
      RVJVKJICYRVCVLVIYNYRFPZJCVDZUVAFPYMYKUVGYLYMUVFJCYMUVFUVCYMYREVMZFYRUVHPY
      MEYQVNVOGFEVPVQZVJVKRJCYRFVRVSVTZFUUAUUSUUSUQZWASKBUUCUUFUUFUQWBVIYNUUGUU
      LUUJUUPYNUUFFBYNUUTYKUUFFNUVJYKYLYMWCFDUUAUUSUVKWESWDYNUUJUUPYNUUIUUPKUUC
      UUHUUCLZUUHYRNZJCOZUUHFNZWFZYNUUIUUPTZUVLUUHUUALZUUHUUBLZWFUVPUUHUUAUUBWG
      UVRUVNUVSUVOYTUVNIUUHKVGYPUUHNYSUVMJCYPUUHYRWHWIWJKFWKWLWMYNUVNUVQUVOYNUV
      MUVQJCYMYKUVCUVMUVQTTYLYMUVCUVMUUIUUPYMUVCUVMUCZUUIQUVCYRBPZUUPUVTUVCUUIY
      MUVCUVMWNWOUVTUUIUWAUVMYMUUIUWAUTUVCUUHYRBWQRWPUUOUWAAYQCUUMYQNUUNYRBUUMY
      QEWRWSWTZSXARXBYLYKUVCJXCZYMUVOUVQTZYLCXDXEUWCCXFJCXIXGYKUWCYMUWDYKUVCYMU
      WDTZJUVCUWETYKUVCYMUVOUUIUUPUVCYMUVOUCZUUIQUVCUWAUUPUWFUVCUUIUVCYMUVOWCWO
      UWFUUIUWAUWFUUIFBPZUWAUVOUVCUUIUWGUTYMUUHFBWQRYMUVCUWGUWATZUVOYMUVFUWHUVI
      YRFBXHVIXJXKXTUWBSXAVOXLXMXNXOXPXBYNUUOUUJACYNUUMCLZUUOUUJYNUWIUUOQZQZUUN
      UUCLUUOUUJUWKUUAUUCUUNUUAUUCPUWKUUAUUBXQVOUWKUUNUUALZUUNYRNZJCOZUWIUWNYNU
      UOUWIUUNUUNNZUWNUUNUQUWMUWOJUUMCYQUUMNYRUUNUUNYQUUMEWRXRWTYAXSYNUWLUWNUTZ
      UWJYMYKUWPYLYMUVDUUNVCLUWPUVEEUUMAVGVHYTUWNIUUNVCYPUUNNYSUWMJCYPUUNYRWHWI
      YBYCRWOYDYEYNUWIUUOYFUUIUUOKUUNUUCUUHUUNBWQWTSYGXBYHYIYJ $.
      $( [1-Dec-2010] $) $( [8-Sep-2009] $)

    elfm2.2 $e |- L = ( filGen ` B ) $.
    $( An element of a mapping filter.  (Contributed by Jeff Hankins,
       26-Sep-2009.) $)
    elfm2 $p |- ( ( X e. C /\ B e. fBas /\ F : Y --> X ) -> ( A e. ( ( X
    FilMap B ) ` F ) <-> ( A C_ X /\ E. x e. L ( F " x ) C_ A ) ) ) $=
      ( vy wcel cfv wss cv cima wrex wa rexlimdv wi cfbas wf w3a co elfm fbssfg
      cfm cfg syl6sseqr imp adantrr 3ad2antl2 simprr wceq imaeq2 sseq1d rcla4ev
      sseld syl11anc exp32 wb eleq2i elfg syl5bb 3ad2ant2 imass2 sstr2 ad2antll
      com12 syl5 reximdv expr com23 expimpd sylbid impbid anbi2d bitrd ) GDLZCU
      ALZHGEUBZUCZBEGCUGUDMLBGNZEKOZPZBNZKCQZRWCEAOZPZBNZAFQZRKBCDEGHIUEWBWGWKW
      CWBWGWKWBWFWKKCWBWDCLZWFWKWBWLWFRZRWDFLZWFWKVTVSWMWNWAVTWLWNWFVTWLWNVTCFW
      DVTCCUHMZFCUFJUIURUJUKULWBWLWFUMWJWFAWDFWHWDUNWIWEBWHWDEUOUPUQUSUTSWBWJWG
      AFWBWHFLZWHHNZWDWHNZKCQZRZWJWGTZVTVSWPWTVAWAWPWHWOLVTWTFWOWHJVBKWHCHIVCVD
      VEWBWQWSXAWBWQRWJWSWGWBWQWJWSWGTWBWQWJRRZWRWFKCWRWEWINZXBWFWDWHEVFWJXCWFT
      WBWQXCWJWFWEWIBVGVIVHVJVKVLVMVNVOSVPVQVR $.
      $( [1-Dec-2010] $) $( [26-Sep-2009] $)

    $( An alternate formulation of elementhood in a mapping filter that
       requires ` F ` to be onto.  (Contributed by Jeff Hankins,
       1-Oct-2009.) $)
    elfm3 $p |- ( ( B e. fBas /\ F : Y -onto-> X ) -> ( A e. ( ( X FilMap B
    ) ` F ) <-> E. x e. L A = ( F " x ) ) ) $=
      ( vy vz wcel cvv cima wceq wb wa adantl adantr wss cfbas wfo cfm cfv wrex
      co cv foima wfun fofun uniexg syl5eqel funimaexg syl11anc eqeltrrd w3a wf
      cuni fof elfm2 syl3an3 ccnv cfil cfg fgfil 3ad2ant2 ad2antrr cdm cnvimass
      simprl wfn fofn fndm syl syl5sseq unieqi syl6eqr sseqtrd 3adant1 3ad2ant3
      fgbas eleq2i syl5bb simprbda sseq2 biimpar sylan 3ad2antl3 adantlr syldan
      elfg funimass3 biimpd impr 3jca eqid fillsb foimacnv eqcomd imaeq2 eqeq2d
      sylc rcla4ev exp32 rexlimdv expimpd simprr imassrn eqsstrd eqimss2 sseq1d
      crn forn sylan2 jca impbid bitrd 3coml mpd3an3 ) CUALZGFDUBZFMLZBDFCUCUFU
      DLZBDAUGZNZOZAEUEZPZXTYAQZDGNZFMYAYJFOXTGFDUHRYIDUIZGMLZYJMLYAYKXTGFDUJZR
      XTYLYAXTGCURMHCUAUKULSDGMUMUNUOYBXTYAYHYBXTYAUPZYCBFTZDJUGZNZBTZJEUEZQZYG
      YAYBXTGFDUQYCYTPGFDUSJBCMDEFGHIUTVAYNYTYGYNYOYSYGYNYOQZYRYGJEUUAYPELZYRYG
      UUAUUBYRQZQZDVBBNZELZBDUUENZOZYGUUDEVCLZUUBUUEEURZTZYPUUETZUPUUFYNUUIYOUU
      CXTYBUUIYAXTECVDUDZVCICVEULVFVGUUDUUBUUKUULUUAUUBYRVJYNUUKYOUUCXTYAUUKYBY
      IUUEGUUJYAUUEGTXTYADVHZUUEGDBVIYADGVKUUNGOZGFDVLGDVMVNZVORYIGUUMURZUUJXTG
      UUQOYACGHWASEUUMIVPVQVRVSVGUUAUUBYRUULUUAUUBQZYRUULUURYKYPUUNTZYRUULPYNYK
      YOUUBYAYBYKXTYMVTVGUUAUUBYPGTZUUSUUAUUBUUTKUGYPTKCUEZUUBYPUUMLZUUAUUTUVAQ
      ZEUUMYPIWBYNUVBUVCPZYOXTYBUVDYAKYPCGHWKVFSWCWDYNUUTUUSYOYAYBUUTUUSXTYAUUO
      UUTUUSUUPUUOUUSUUTUUNGYPWEWFWGWHWIWJYPBDWLUNWMWNWOYPUUEEUUJUUJWPWQXBUUAUU
      HUUCYAYBYOUUHXTYAYOQUUGBGFBDWRWSWHSYFUUHAUUEEYDUUEOYEUUGBYDUUEDWTXAXCUNXD
      XEXFYNYFYTAEYNYDELZYFYTYNUVEYFQZQZYOYSUVGBYEFYNUVEYFXGUVGDXLZYEFDYDXHYNUV
      HFOZUVFYAYBUVIXTGFDXMVTSVOXIUVFYSYNYFUVEYEBTZYSYEBXJYRUVJJYDEYPYDOYQYEBYP
      YDDWTXKXCXNRXOXDXEXPXQXRXS $.
      $( [1-Dec-2010] $) $( [1-Oct-2009] $)
  $}

  ${
    $d s x B $.  $d s x C $.  $d s x F $.  $d s x L $.  $d s x X $.
    $d s x Y $.
    fbfgfmeq.1 $e |- Y = U. B $.
    fbfgfmeq.2 $e |- L = ( filGen ` B ) $.
    $( The image filter of a filter base is the same as the image filter of its
       generated filter.  (Contributed by Jeff Hankins, 18-Nov-2009.) $)
    fbfgfmeq $p |- ( ( X e. C /\ B e. fBas /\ F : Y --> X ) -> ( ( X FilMap B )
    ` F ) = ( ( X FilMap L ) ` F ) ) $=
      ( vx vs wcel cfbas wf cfm co cfv cv wss cuni wb w3a cima wrex elfm2 simp1
      wa cfg cfil fgfil syl5eqel filfbas syl 3ad2ant2 fgbas feq2d adantl unieqi
      biimp3a eqcomi elfm syl111anc bitr4d eqrdv ) EBKZALKZFECMZUAZICEANOPZCEDN
      OPZVGIQZVHKVJERCJQUBVJRJDUCUFZVJVIKZJVJABCDEFGHUDVGVDDLKZAUGPZSZECMZVLVKT
      VDVEVFUEVEVDVMVFVEDUHKVMVEDVNUHHAUIUJDUKULUMVDVEVFVPVEVFVPTVDVEFVOECAFGUN
      UOUPURJVJDBCEVODSVODVNHUQUSUTVAVBVC $.
      $( [18-Nov-2009] $)
  $}

  ${
    $d f x y J $.  $d f F $.  $d f x y X $.  $d f y Y $.  $d f y L $.
    flffval.1 $e |- X = U. J $.
    flffval.2 $e |- Y = U. L $.
    $( Given a topology and a filtered set, return the convergence function on
       the functions from the filtered set to the base set of the topological
       space.  (Contributed by Jeff Hankins, 14-Oct-2009.)  (Revised by Mario
       Carneiro, 15-Dec-2013.) $)
    flffval $p |- ( ( J e. Top /\ L e. Fil ) -> ( J fLimf L ) =
        ( f e. ( X ^m Y ) |-> ( ( fLim ` J ) ` ( ( X FilMap L ) ` f ) ) ) ) $=
      ( vx vy cv cuni cmap co cfm cfv cflim cmpt wceq unieq cvv syl6eqr opreq1d
      ctop cfil cflf fveq1d fveq12d mpteq12dv opreq2d opreq2 fveq2d df-flf wcel
      fveq2 oprex mptexg ax-mp ovmpt2 ) HIBCUCUDAHJZKZIJZKZLMZAJZUTVANMZOZUSPOZ
      OZQADELMZVDDCNMZOZBPOZOZQZUEADVBLMZVDDVANMZOZVLOZQUSBRZAVCVHVOVRVSUTDVBLV
      SUTBKDUSBSFUAZUBVSVFVQVGVLUSBPUNVSVDVEVPVSUTDVANVTUBUFUGUHVACRZAVOVRVIVMW
      AVBEDLWAVBCKEVACSGUAUIWAVQVKVLWAVDVPVJVACDNUJUFUKUHHIAULVITUMVNTUMDELUOAV
      IVMTUPUQUR $.
      $( [15-Dec-2013] $) $( [14-Oct-2009] $)

    $( Given a function from a filtered set to a topological space, define the
       set of limit points of the function.  (Contributed by Jeff Hankins,
       8-Nov-2009.) $)
    flfval $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) ->
      ( ( J fLimf L ) ` F ) = ( ( fLim ` J ) ` ( ( X FilMap L ) ` F ) ) ) $=
      ( vf ctop wcel cfil wf cflf co cfv cfm cflim wceq wa cmap wb biimpar cmpt
      topopn filusb elmapg syl2an cv flffval fveq1d fveq2 fveq2d fvmpt sylan9eq
      eqid fvex syldan 3impa ) BIJZCKJZEDALZABCMNZOZADCPNZOZBQOZOZRZUSUTSZVAADE
      TNZJZVHVIVKVAUSDBJECJVKVAUAUTBDFUDCEGUEDEABCUFUGUBVIVKVCAHVJHUHZVDOZVFOZU
      CZOVGVIAVBVOHBCDEFGUIUJHAVNVGVJVOVLARVMVEVFVLAVDUKULVOUOVEVFUPUMUNUQUR $.
      $( [15-Dec-2013] $) $( [8-Nov-2009] $)
  $}

  ${
    $d n A $.  $d n s F $.  $d n J $.  $d n s L $.  $d n s X $.  $d n s Y $.
    flimfnei.1 $e |- X = U. J $.
    flimfnei.2 $e |- Y = U. L $.
    $( The property of being a limit point of a function in terms of
       neighborhoods.  (Contributed by Jeff Hankins, 9-Nov-2009.) $)
    flimfnei $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) -> ( A e. ( ( J
    fLimf L ) ` F ) <-> ( A e. X /\ A. n e. ( ( nei ` J ) ` { A } ) E. s e. L (
    F " s ) C_ n ) ) ) $=
      ( ctop wcel cfil co cfv wss wa wb cvv syl111anc wf w3a cflf cfm cflim csn
      cnei cima wrex wral flfval eleq2d cuni wceq simp1 cfbas syl5eqel 3ad2ant1
      cv uniexg filfbas 3ad2ant2 simp3 fmfil fmbas eqcomd eqid elflim 3ad2antl1
      dfss3 neii1 id elfm syl3an adantr ibar adantl bitr4d syldan syl5bb anbi2d
      ralbidva 3bitrd ) DKLZEMLZGFCUAZUBZACDEUCNOZLACFEUDNOZDUEOOZLZAFLZAUFZDUG
      OOZWIPZQZWLCHUSUHBUSZPHEUIZBWNUJZQWGWHWJACDEFGIJUKULWGWDWIMLZFWIUMZUNWKWP
      RWDWEWFUOWGFSLZEUPLZWFWTWDWEXBWFWDFDUMSIDKUTUQZURZWEWDXCWFEVAZVBZWDWEWFVC
      ZSECFGJVDTWGXAFWGXBXCWFXAFUNXEXGXHSECFGJVETVFAWIDFXAIXAVGVHTWGWOWSWLWOWQW
      ILZBWNUJWGWSBWNWIVJWGXIWRBWNWGWQWNLZWQFPZXIWRRWDWEXJXKWFWMDWQFIVKVIWGXKQX
      IXKWRQZWRWGXIXLRZXKWDXBWEXCWFWFXMXDXFWFVLHWQESCFGJVMVNVOXKWRXLRWGXKWRVPVQ
      VRVSWBVTWAWC $.
      $( [1-Dec-2010] $) $( [9-Nov-2009] $)
  $}

  ${
    $d n A $.  $d n s F $.  $d n J $.  $d n s L $.  $d n s N $.  $d n s X $.
    $d n s Y $.
    flimfneii.1 $e |- X = U. J $.
    flimfneii.2 $e |- Y = U. L $.
    $( A neighborhood of a limit point of a function contains the image of a
       filter element.  (Contributed by Jeff Hankins, 11-Nov-2009.) $)
    flimfneii $p |- ( ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) /\ A e. ( ( J
    fLimf L ) ` F ) /\ N e. ( ( nei ` J ) ` { A } ) ) -> E. s e. L ( F " s ) C_
    N ) $=
      ( vn ctop wcel cfil wf w3a cfv cv wss wrex cflf co csn cnei cima flimfnei
      wral simplbda 3adant3 wi wceq sseq2 rexbidv rcla4v 3ad2ant3 mpd ) CLMDNMG
      FBOPZABCDUAUBQMZEAUCCUDQQZMZPBHRUEZKRZSZHDTZKUSUGZVAESZHDTZUQURVEUTUQURAF
      MVEAKBCDFGHIJUFUHUIUTUQVEVGUJURVDVGKEUSVBEUKVCVFHDVBEVAULUMUNUOUP $.
      $( [1-Dec-2010] $) $( [11-Nov-2009] $)
  $}

  ${
    $d n o s A $.  $d n o s F $.  $d n o s J $.  $d n o s X $.  $d n o s Y $.
    flimopn.1 $e |- X = U. J $.
    flimopn.2 $e |- Y = U. F $.
    $( The condition for being a limit point of a filter still holds if one
       only considers open neighborhoods.  (Contributed by Jeff Hankins,
       4-Sep-2009.) $)
    flimopn $p |- ( ( ( J e. Top /\ F e. Fil /\ X = Y ) /\ A e. X ) -> ( A e. (
    ( fLim ` J ) ` F ) <-> A. o e. J ( A e. o -> o e. F ) ) ) $=
      ( vn vs wcel wa cfv wss cv wi wb ex adantr com23 ctop cfil wceq w3a cflim
      csn cnei wral flimelbas pm4.71rd elflim bitr3d pm5.32 sylibr opnneip ssel
      imp 3expib syl9 exp4a 3ad2antl1 ralrimdv snssi isnei sylan2 eleq2 imbi12d
      wrex eleq1 rcla4v adantl snssg anbi1d ad2antlr pm2.27 sseq2 fillsb sylbid
      3expd com24 3adant1 ad2antrr com13 syl6 com12 imp3a sylbird syld rexlimdv
      ssrdv impbid bitrd ) DUAKZCUBKZEFUCZUDZAEKZLZACDUEMMKZAUFZDUGMMZCNZABOZKZ
      XCCKZPZBDUHZWPWQWSXBQZWPWQWSLZWQXBLZQWQXHPWPWSXIXJWPWSWQWPWSWQACDEFGHUIRU
      JACDEFGHUKULWQWSXBUMUNUQWRXBXGWRXBXFBDWMWNWQXBXCDKZXFPPWOWMWQLZXBXKXDXEXL
      XKXDLZXCXAKZXBXEWMXMXNPWQWMXKXDXNADXCUOURSXACXCUPUSUTVAVBWRXGXBWRXGLZIXAC
      XOIOZXAKZXPENZWTJOZNZXSXPNZLZJDVHZLZXPCKZWRXQYDQZXGWMWNWQYFWOWQWMWTENYFAE
      VCWTJDXPEGVDVEVASXOXRYCYEXOYCXRYEXOYBXRYEPZJDWRXGXSDKZYBYGPZPWRYHXGYIWRYH
      XGYIPWRYHLZXGAXSKZXSCKZPZYIYHXGYMPWRXFYMBXSDXCXSUCXDYKXEYLXCXSAVFXCXSCVIV
      GVJVKYJYBYMYGYJYBYKYALZYMYGPZWQYNYBQWPYHWQYKXTYAAXSEVLVMVNYJYKYAYOYKYJYAY
      OPYKYMYAYJYGYKYMYLYAYJYGPPYKYLVOYJYAYLYGWPYAYLYGPPZWQYHWNWOYPWMWNWOLZXRYL
      YAYEYQXRXPFNZYLYAYEPZPZWOXRYRQWNEFXPVPVKWNYRYTPWOWNYLYRYSWNYLYRYAYEXSXPCF
      HVQVSTSVRVTWAWBWCWDVTWEWFWGTWHRTUQWITWFVRWJRWKWL $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    $d o A $.  $d o x B $.  $d o F $.  $d o J $.  $d o X $.  $d o Y $.
    fbaslim.1 $e |- X = U. J $.
    fbaslim.2 $e |- Y = U. B $.
    fbaslim.3 $e |- F = ( filGen ` B ) $.
    $( A condition for a filter to converge to a point involving one of its
       bases.  (Contributed by Jeff Hankins, 4-Sep-2009.) $)
    fbaslim $p |- ( ( ( J e. Top /\ B e. fBas /\ X = Y ) /\ A e. X ) -> ( A e.
    ( ( fLim ` J ) ` F ) <-> A. o e. J ( A e. o -> E. x e. B x C_ o ) ) ) $=
      ( wcel wceq w3a wa cfv wi wss wb adantr ctop cfbas cv wral wrex cfil cuni
      cflim simp1 fgfil syl5eqel 3ad2ant2 simp3 fgbas unieqi syl6eqr eqtrd 3jca
      cfg eqid flimopn sylan eltopss 3ad2ant1 sseq2 3ad2ant3 biimpa eleq2i elfg
      ex syl5bb ibar adantl bitr4d 3ad2antl2 syldan syld pm5.74d ralbidv2 bitrd
      imbi2d ) FUALZCUBLZGHMZNZBGLZOZBEFUHPPLZBDUCZLZWIELZQZDFUDZWJAUCWIRACUEZQ
      ZDFUDWEWBEUFLZGEUGZMZNWFWHWMSWEWBWPWRWBWCWDUIWCWBWPWDWCECUSPZUFKCUJUKULWE
      GHWQWBWCWDUMWCWBHWQMWDWCHWSUGWQCHJUNEWSKUOUPULUQURBDEFGWQIWQUTVAVBWGWLWOD
      FFWGWIFLZWLWOWGWTWIGRZWLWOSZWEWTXAQZWFWBWCXCWDWBWTXAWIFGIVCVJVDTWEXAXBQWF
      WEXAXBWEXAOWKWNWJWEXAWIHRZWKWNSZWEXAXDWDWBXAXDSWCGHWIVEVFVGWCWBXDXEWDWCXD
      OWKXDWNOZWNWCWKXFSXDWKWIWSLWCXFEWSWIKVHAWICHJVIVKTXDWNXFSWCXDWNVLVMVNVOVP
      WAVJTVQVRVSVT $.
      $( [4-Sep-2009] $)
  $}

  ${
    $d o A $.  $d o s F $.  $d o J $.  $d o s L $.  $d o s X $.  $d o s Y $.
    isflimf.1 $e |- X = U. J $.
    isflimf.2 $e |- Y = U. L $.
    $( The property of being a limit point of a function.  (Contributed by Jeff
       Hankins, 8-Nov-2009.) $)
    isflimf $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) -> ( A e. ( ( J
    fLimf L ) ` F ) <-> ( A e. X /\ A. o e. J ( A e. o -> E. s e. L ( F " s )
    C_ o ) ) ) ) $=
      ( ctop wcel cfil w3a co cfv wa cvv syl111anc wb wf cflf cfm cflim cv wral
      cima wss wrex flfval eleq2d simpl cuni wceq simp1 cfbas syl5eqel 3ad2ant1
      uniexg filfbas 3ad2ant2 simp3 fmfil fmbas eqcomd 3jca flimelbas sylan jca
      wi eqid anim2i flimopn ibar adantl pm5.21nd elfm adantr elssuni syl6sseqr
      bitrd biantrurd bitr4d imbi2d ralbidva anbi2d 3bitrd ) DKLZEMLZGFCUAZNZAC
      DEUBOPZLACFEUCOPZDUDPPZLZAFLZABUEZLZWQWMLZVJZBDUFZQZWPWRCHUEUGWQUHHEUIZVJ
      ZBDUFZQWKWLWNACDEFGIJUJUKWKWOXBWKWPQZWKWOQWKWPWKWOULWKWHWMMLZFWMUMZUNZNZW
      OWPWKWHXGXIWHWIWJUOWKFRLZEUPLZWJXGWHWIXKWJWHFDUMZRIDKUSUQURZWIWHXLWJEUTVA
      ZWHWIWJVBZRECFGJVCSWKXKXLWJXIXNXOXPXKXLWJNXHFRECFGJVDVESVFZAWMDFXHIXHVKZV
      GVHVIXBWPWKWPXAULVLXFWOXAXBWKXJWPWOXATXQABWMDFXHIXRVMVHWPXAXBTWKWPXAVNVOW
      AVPWKXAXEWPWKWTXDBDWKWQDLZQZWSXCWRXTWSWQFUHZXCQZXCWKWSYBTZXSWKXKXLWJYCXNX
      OXPHWQERCFGJVQSVRXSXCYBTWKXSYAXCXSWQXMFWQDVSIVTWBVOWCWDWEWFWG $.
      $( [1-Dec-2010] $) $( [8-Nov-2009] $)
  $}

  ${
    $d o s A $.  $d o s F $.  $d o s J $.  $d o s L $.  $d o s X $.
    $d o s Y $.
    flimfelbas.1 $e |- X = U. J $.
    flimfelbas.2 $e |- Y = U. L $.
    $( A limit point of a function is in the topological space.  (Contributed
       by Jeff Hankins, 10-Nov-2009.) $)
    flimfelbas $p |- ( ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) /\ A e. ( ( J
    fLimf L ) ` F ) ) -> A e. X ) $=
      ( vo vs ctop wcel cfil wf w3a cflf co cfv cv cima wss wi isflimf simprbda
      wrex wral ) CKLDMLFEBNOABCDPQRLAELAISZLBJSTUGUAJDUEUBICUFAIBCDEFJGHUCUD
      $.
      $( [1-Dec-2010] $) $( [10-Nov-2009] $)
  $}


  ${
    $d F f x $.  $d J f x $.  $d X f x $.
    hausflim2.1 $e |- X = U. J $.
    $( A condition for a topology to be Hausdorff in terms of filters.  A
       topology is Hausdorff iff every filter has at most one limit point.
       (Contributed by FL, 6-Dec-2010.) $)
    hausflim2 $p |- ( ( J e. Haus /\ F e. Fil /\ X = U. F ) -> E*
    x x e. ( ( fLim ` J ) ` F ) ) $=
      ( vf cha wcel cfil cuni wceq cv cflim cfv wmo ctop haustop wral hausflim
      wi unieq eqeq2d ax-17 fveq2 eleq2d mobid imbi12d rcla4cv syl6bi mpcom
      3imp ) CGHZBIHZDBJZKZALZBCMNZNZHZAOZCPHZULUMUOUTTZTZCQVAULDFLZJZKZUPVDUQN
      ZHZAOZTZFIRVCAFCDESVJVBFBIVDBKZVFUOVIUTVKVEUNDVDBUAUBVKVHUSAVKAUCVKVGURUP
      VDBUQUDUEUFUGUHUIUJUK $.
      $( [14-Nov-2010] $)
  $}

  ${
    holimf.1 $e |- Y = U. L $.
    holimf.2 $e |- X = U. J $.
    ${
      $d F x $.  $d J x $.  $d L x $.  $d X x $.  $d Y x $.
      $( If a function has its values in a Hausdorff space then it has at most
         one limit value.  (Contributed by FL, 6-Dec-2010.) $)
      holimf $p |- ( ( L e. Fil /\ J e. Haus /\ F : Y --> X )
       -> E* x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cha wf co cfv wmo cuni wceq cvv 3ad2ant2 syl111anc w3a cflf
        cv cflim simp2 cfbas uniexg syl5eqel filfbas 3ad2ant1 simp3 fmfil fmbas
        eqcomd hausflim2 ax-17 ctop haustop simp1 flfval eleq2d mobid mpbird
        cfm ) DIJZCKJZFEBLZUAZAUCZBCDUBMNZJZAOVIBEDVDMNZCUDNNZJZAOZVHVFVLIJZEVL
        PZQVOVEVFVGUEVHERJZDUFJZVGVPVFVEVRVGVFECPRHCKUGUHSZVEVFVSVGDUIUJZVEVFVG
        UKZRDBEFGULTVHVQEVHVRVSVGVQEQVTWAWBRDBEFGUMTUNAVLCEHUOTVHVKVNAVHAUPVHVJ
        VMVIVHCUQJZVEVGVJVMQVFVEWCVGCURSVEVFVGUSWBBCDEFHGUTTVAVBVC $.
        $( [14-Nov-2010] $)
    $}

    ${
      $d F x $.  $d J x $.  $d L x $.  $d X x $.  $d Y x $.
      $( If a convergent function has its values in a Hausdorff space then it
         has only one limit value.  (Contributed by FL, 6-Dec-2010.) $)
      holimf2 $p |- ( ( ( L e. Fil /\ J e. Haus /\ F : Y --> X ) /\
                   ( ( J fLimf L ) ` F ) =/= (/) )
       -> E! x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cha wf w3a cflf co cfv c0 wne wa cv holimf adantr wb n0moeu
        wmo weu adantl mpbid ) DIJCKJFEBLMZBCDNOPZQRZSATUJJZAUEZULAUFZUIUMUKABC
        DEFGHUAUBUKUMUNUCUIAUJUDUGUH $.
        $( [14-Nov-2010] $)
    $}
  $}


