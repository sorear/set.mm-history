$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c fLim1 $.

  $( Extend class notation with a function returning the limit of a filter. $)
  cflim1 $a class fLim1 $.

  ${
    $d x y a b l $.
    $( Define a function (indexed by a topology ` x ` ) whose value is the
       limits of a filter ` a ` . $)
    df-flim1 $a |- fLim1 = { <. x , y >. | ( x e. Top /\ y = { <. a , b >. |
      ( a e. Fil /\ U. a = U. x /\
        b = { l e. U. x | ( ( nei ` x ) ` { l } ) C_ a } ) } ) } $.
  $}

  ${
    $d J a b l x y $.  $d X a b l x y $.
    sfvlim.1 $e |- X = U. J $.
    $( Functions whose values are the limits of the filters.  (Contributed by
       FL, 1-Sep-2008.) $)
    sfvlim $p |- ( J e. Top -> ( fLim1 ` J ) = { <. a , b >. |
      ( a e. Fil /\ U. a = X /\
        b = { l e. X | ( ( nei ` J ) ` { l } ) C_ a } ) } ) $=
      ( vx vy ctop wcel cv cfil cuni wceq csn cnei cfv wss crab w3a copab cvv
      cflim1 cpw cin wa ssexg wi wal simpll eqimss sspwuni visset elpw biimpri
      sylbir syl ad2antlr jca elin sylibr ex 3adant3 impcom 3simp3 adantl
      19.21aivv ssopab2 uniexg syl5eqel pwexg 3syl inex1g incom eleq1i biimpi
      opabex2g sylanc unieq syl6eqr eqeq2d rabeq fveq2 fveq1d sseq1d rabbidv
      eqtrd 3anbi23d opabbidv df-flim1 fvopab4g mpdan ) AIJZCKZLJZWNMZBNZDKZEKO
      ZAPQZQZWNRZEBSZNZTZCDUAZUBJZAUCQXFNXFWNLBUDZUDZUEZJZXDUFZCDUAZRZXMUBJZXGW
      MXFXMUBUGWMXEXLUHZDUICUIXNWMXPCDWMXEXLWMXEUFXKXDXEWMXKWOWQWMXKUHXDWOWQUFZ
      WMXKXQWMUFZWOWNXIJZUFXKXRWOXSWOWQWMUJWQXSWOWMWQWPBRZXSWPBUKXTWNXHRZXSWNBU
      LXSYAWNXHCUMUNUOUPUQURUSWNLXIUTVAVBVCVDXEXDWMWOWQXDVEVFUSVBVGXEXLCDVHVAWM
      XJUBJZXOWMXIUBJZXILUEZUBJZYBWMBUBJXHUBJYCWMAMZUBBAIVIFVJBUBVKXHUBVKVLXILU
      BVMYEYBYDXJUBXILVNVOVPVLCDXJXCUBVQUQVRGHAWOWPGKZMZNZWRWSYGPQZQZWNRZEYHSZN
      ZTZCDUAXFIUBUCYGANZYOXECDYPYIWQYNXDWOYPYHBWPYPYHYFBYGAVSFVTZWAYPYMXCWRYPY
      MYLEBSZXCYPYHBNYMYRNYQYLEYHBWBUQYPYLXBEBYPYKXAWNYPWSYJWTYGAPWCWDWEWFWGWAW
      HWIGHCDEWJWKWL $.
      $( [1-Dec-2010] $) $( [17-Aug-2008] $)
  $}


  ${
    $d l x y F $.  $d l x y J $.  $d l x y X $.  $d x y Y $.
    limfil.1 $e |- X = U. J $.
    limfil.2 $e |- Y = U. F $.
    $( The set of limit points of a filter.  (Contributed by Jeff Hankins,
       4-Sep-2009.) $)
    limfil $p |- ( ( J e. Top /\ F e. Fil /\ X = Y ) -> ( ( fLim1 ` J ) ` F ) =
    { l e. X | ( ( nei ` J ) ` { l } ) C_ F } ) $=
      ( vx vy ctop wcel cfil wceq w3a cflim1 cfv cv cuni csn cnei wss crab
      copab sfvlim fveq1d 3ad2ant1 cvv wfun wa cop funopfvg uniexg syl5eqel
      rabexg syl 3simp3 ssopab2i funopabeq funss mp2 jctir 3simp2 eqcom biimpi
      3ad2ant3 eqidd 3jca wb eleq1 unieq syl6eqr eqeq1d sseq2 rabbidv eqeq2d
      3anbi123d eqeq1 3anbi3d opelopabg sylan2 ancoms sylan 3adant3 mpbird
      sylc eqtrd ) BJKZALKZCDMZNZABOPZPZAHQZLKZWMRZCMZIQZEQSBTPPZWMUAZECUBZMZNZ
      HIUCZPZWRAUAZECUBZWGWHWLXDMWIWGAWKXCBCHIEFUDUEUFXFUGKZXCUHZUIAXFUJXCKZXDX
      FMWJAXFUGXCUKWJXGXHWGWHXGWIWGCUGKZXGWGBRUGCBJULFUMZXEECUGUNZUOUFXCXAHIUCZ
      UAXMUHXHXBXAHIWNWPXAUPUQHIWTURXCXMUSUTVAWJXIWHDCMZXFXFMZNZWJWHXNXOWGWHWIV
      BWIWGXNWHWIXNCDVCVDVEWJXFVFVGWGWHXIXPVHZWIXJWHXQWGWHXJXQWHXGXQXJXBWHXNWQX
      FMZNXPHIAXFLUGWMAMZWNWHWPXNXAXRWMALVIXSWODCXSWOARDWMAVJGVKVLXSWTXFWQXSWSX
      EECWMAWRVMVNVOVPXRXRXOWHXNWQXFXFVQVRVSXLVTWAXKWBWCWDWEWF $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    $d l A $.  $d l F $.  $d l J $.  $d l X $.  $d l Y $.
    isfillim.1 $e |- X = U. J $.
    isfillim.2 $e |- Y = U. F $.
    $( The predicate "is a limit point of a filter."  (Contributed by Jeff
       Hankins, 4-Sep-2009.) $)
    isfillim $p |- ( ( J e. Top /\ F e. Fil /\ X = Y ) -> ( A e. ( ( fLim1 ` J
    ) ` F ) <-> ( A e. X /\ ( ( nei ` J ) ` { A } ) C_ F ) ) ) $=
      ( vl ctop wcel cfil wceq w3a cflim1 cfv cv csn cnei wss crab wa limfil
      eleq2d sneq fveq2d sseq1d elrab syl6bb ) CIJBKJDELMZABCNOOZJAHPZQZCROZOZB
      SZHDTZJADJAQZUMOZBSZUAUIUJUPABCDEHFGUBUCUOUSHADUKALZUNURBUTULUQUMUKAUDUEU
      FUGUH $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    limfilss.1 $e |- X = U. J $.
    limfilss.2 $e |- Y = U. F $.
    limfilss.3 $e |- Z = U. G $.
    $( A limit point of a filter is a limit point of a finer filter.
       (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    limfilss $p |- ( ( ( ( J e. Top /\ F e. Fil /\ G e. Fil ) /\ X = Y /\ X = Z
    ) /\ F C_ G /\ A e. ( ( fLim1 ` J ) ` F ) ) -> A e. ( ( fLim1 ` J ) ` G ) )
        $=
      ( ctop wcel cfil w3a wceq wss cflim1 cfv wa csn cnei wi sstr2 com12
      anim2d adantl wb isfillim 3expa 3adantl3 3adant3 adantr 3adantl2 3adant2
      3imtr4d 3impia ) DKLZBMLZCMLZNZEFOZEGOZNZBCPZABDQRZRLZACVERLZVCVDSAELZATD
      UARRZBPZSZVHVICPZSZVFVGVDVKVMUBVCVDVJVLVHVJVDVLVIBCUCUDUEUFVCVFVKUGZVDUTV
      AVNVBUQURVAVNUSUQURVAVNABDEFHIUHUIUJUKULVCVGVMUGZVDUTVBVOVAUQUSVBVOURUQUS
      VBVOACDEGHJUHUIUMUNULUOUP $.
      $( [5-Sep-2009] $)
  $}

  ${
    flimelbas.1 $e |- X = U. J $.
    flimelbas.2 $e |- Y = U. F $.
    $( A limit point of a filter belongs to its base set.  (Contributed by Jeff
       Hankins, 4-Sep-2009.) $)
    flimelbas $p |- ( ( ( J e. Top /\ F e. Fil /\ X = Y ) /\ A e. ( ( fLim1 ` J
    ) ` F ) ) -> A e. X ) $=
      ( ctop wcel cfil wceq w3a cflim1 cfv csn cnei wss isfillim pm3.26bda ) CH
      IBJIDEKLABCMNNIADIAOCPNNBQABCDEFGRS $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    flimnei.1 $e |- X = U. J $.
    flimnei.2 $e |- Y = U. F $.
    $( A filter contains all of the neighborhoods of its limit points.
       (Contributed by Jeff Hankins, 4-Sep-2009.) $)
    flimnei $p |- ( ( ( J e. Top /\ F e. Fil /\ X = Y ) /\ A e. ( ( fLim1 ` J )
    ` F ) /\ N e. ( ( nei ` J ) ` { A } ) ) -> N e. F ) $=
      ( ctop wcel cfil wceq w3a cflim1 cfv csn cnei wa wss isfillim pm3.27bda
      sseld 3impia ) CIJBKJEFLMZABCNOOJZDAPCQOOZJDBJUDUERUFBDUDUEAEJUFBSABCEFGH
      TUAUBUC $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    $d J x y $.  $d S x y $.  $d X x y $.
    neifil.1 $e |- X = U. J $.
    $( The neighborhoods of a non empty set is a filter.  Bourbaki TG I.36,
       example 2.  (Contributed by FL, 19-Sep-2007.) $)
    neifil $p |- ( ( J e. Top /\ S C_ X /\ S =/= (/) ) ->
      ( ( nei ` J ) ` S ) e. Fil ) $=
      ( vx vy ctop wcel wss c0 wne w3a cnei cfv wn cuni wa cv wi wal cin wral
      cfil 0nnei 3adant2 unnei tpnei biimpa eqeltrd 3adant3 jca wceq sseq2
      ssnei2 exp43 adantr com14 syl6bi com23 3impd mpcom 19.21aivv innei
      3expib 3ad2ant1 r19.21aivv 3jca cvv wb fvex eqid isfil ax-mp sylibr ) BGH
      ZACIZAJKZLZJABMNZNZHOZVTPZVTHZQZERZVTHZFRZWBIZWEWGIZLZWGVTHZSZFTETZWEWGUA
      VTHZFVTUBEVTUBZLZVTUCHZVRWDWMWOVRWAWCVOVQWAVPABUDUEVOVPWCVQVOVPQZWBCVTABC
      DUFZVOVPCVTHABCDUGUHUIUJUKVRWLEFVOVPWLVQWBCULZWRWLWSWTWJWRWKWTWFWHWIWRWKS
      ZWTWHWFWIXASZWTWHWGCIZWFXBSWBCWGUMWRWFWIXCWKVOWFWIXCWKSSSVPVOWFWIXCWKABWG
      WECDUNUOUPUQURUSUTUSVAUJVBVRWNEFVTVTVOVPWFWKQWNSVQVOWFWKWNABWGWEVCVDVEVFV
      GVTVHHWQWPVIAVSVJEFVHVTWBWBVKVLVMVN $.
      $( [1-Dec-2010] $) $( [18-Sep-2007] $)
  $}

  ${
    $d f u v w x y z J $.  $d f u v w x y z X $.
    hausfillim.1 $e |- X = U. J $.
    $( A condition for a topology to be Hausdorff in terms of filters.  A
       topology is Hausdorff iff every filter has at most one limit point.
       (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    hausfillim $p |- ( J e. Top -> ( J e. Haus <-> A. f e. Fil ( X = U. f -> E*
    x x e. ( ( fLim1 ` J ) ` f ) ) ) ) $=
      ( vz vw vu vv vy ctop wcel cha cv wne cin c0 wceq csn cnei cfv wrex wi
      wral cuni cflim1 wmo cfil hausnei2 wa wal w3a eqid flimelbas ex anim12d
      imp neeq1 sneq fveq2d rexeqdv imbi12d neeq2 rexbidv rcla42v syl wn
      filesn 3ad2ant2 adantr pm2.27 3simp3 filint 3simp2 ad2antrr flimnei
      3expa adantrr adantlrr adantrl adantlrl syl3anc 3adant3 eqeltrrd 3exp
      com3l r19.23aivv syl6 com13 impr df-ne syl5ibr mt3d expr syld com23
      19.21adv 3expib exp4a r19.21adv wex cun cfi cfg cfbas wnel wss snssi wb
      tpnei 3ad2ant1 mpbid ne0i ssun1 ssn0 mpan pm3.27 2ralbii sylan2br visset
      snnz neifil mp3an3 sylan2 filfbas jca fbunfip mpbird cvv fvex unex a1i
      fsubbas fgfil unnei uneq12d unidm syl6req fiuni uniun syl5eqr ax-mp
      fgbas eqtrd adantl simpll ad2antrl abfi2 sstri fbssfg imp3a sstrd
      isfillim exp520 com24 3impib impcom simplr ssun2 biimpi 3ad2ant3
      ad2antlr eleq1 anbi1d eqeq1 notbid anbi12d anbi2d eqeq2 sylan9bb cla42ev
      sylan31c jcai unieq eqeq2d fveq2 eleq2d 2exbidv rcla4ev syldan expcom
      mpdd exanali exbii exnal bitri anbi2i annim rexbii rexnal syl6ib ralnex
      bicomi ralbii bitr3i syl5ib con4d r19.21advv impbid mo4 imbi2i syl6bbr
      bitrd ) CKLZCMLFNZGNZOZHNZINZPZQRZIUXFSZCTUAZUAZUBZHUXESZUXMUAZUBZUCZGDUD
      FDUDZDBNZUEZRZANZUYACUFUAZUAZLZAUGZUCZBUHUDZFGIHCDEUIUXDUXTUYCUYGJNZUYFLZ
      UJZUYDUYKRZUCZJUKZAUKZUCZBUHUDZUYJUXDUXTUYSUXDUXTUYRBUHUXDUXTUYAUHLZUYCUY
      QUXDUYTUYCUJUXTUYQUXDUYTUYCUXTUYQUCUXDUYTUYCULZUXTUYPAVUAUXTUYOJVUAUYMUXT
      UYNVUAUYMUXTUYNUCVUAUYMUJZUXTUYDUYKOZUXKIUYKSZUXMUAZUBZHUYDSZUXMUAZUBZUCZ
      UYNVUBUYDDLZUYKDLZUJZUXTVUJUCVUAUYMVUMVUAUYGVUKUYLVULVUAUYGVUKUYDUYACDUYB
      EUYBUMZUNUOVUAUYLVULUYKUYACDUYBEVUNUNUOUPUQUXSVUJUYDUXFOZUXOHVUHUBZUCFGUY
      DUYKDDUXEUYDRZUXGVUOUXRVUPUXEUYDUXFURVUQUXOHUXQVUHVUQUXPVUGUXMUXEUYDUSUTV
      AVBUXFUYKRZVUOVUCVUPVUIUXFUYKUYDVCVURUXOVUFHVUHVURUXKIUXNVUEVURUXLVUDUXMU
      XFUYKUSUTVAVDVBVEVFVUAUYMVUJUYNVUAUYMVUJUJZUJZUYNQUYALZVUAVVAVGZVUSUYTUXD
      VVBUYCUYAVHVIVJVUTVUCVVAUYNVGZVUAUYMVUJVUCVVAUCVUCVUJVUBVVAVUCVUJVUIVUBVV
      AUCZVUCVUIVKUXKVVDHIVUHVUEVUBUXHVUHLZUXIVUELZUJZUXKVVAVUBVVGUXKVVAVUBVVGU
      XKULUXJQUYAVUBVVGUXKVLVUBVVGUXJUYALZUXKUYTUXHUYALZUXIUYALZVVHVUBVVGUJUXHU
      XIUYAVMVUAUYTUYMVVGUXDUYTUYCVNVOVUAUYGVVGVVIUYLVUAUYGUJVVEVVIVVFVUAUYGVVE
      VVIUYDUYACUXHDUYBEVUNVPVQVRVSVUAUYLVVGVVJUYGVUAUYLUJVVFVVJVVEVUAUYLVVFVVJ
      UYKUYACUXIDUYBEVUNVPVQVTWAWBWCWDWEWFWGWHWIWJUYDUYKWKWLWMWNWOUOWPWQWQWRWPW
      SWTUXDUYSUXSFGDDUXDUXGUXEDLZUXFDLZUJZUYSUXRUXDVVMUXGUYSUXRUCZUXDVVMUXGVVN
      UXDVVMUXGULZUXRUYSVVOUXKVGZIUXNUDZHUXQUDZUYSVGZUXRVGZVVOVVRUYCUYMVVCUJZJX
      AZAXAZUJZBUHUBZVVSVVOVVRUXQUXNXBZXCUAZXDUAZUHLZVWEVVOVVRVWIVVOVVRUJZVWGXE
      LZVWIVWJVWKVWFQOZQVWGXFZUJZVWJVWLVWMVWJUXQQOZVWLVVOVWOVVRVVODUXQLZVWOVVOU
      XPDXGZVWPVVMUXDVWQUXGVVKVWQVVLUXEDXHZVJVIUXDVVMVWQVWPXIUXGUXPCDEXJXKXLUXQ
      DXMVFVJUXQVWFXGVWOVWLUXQUXNXNZUXQVWFXOXPVFVWJVWMUXJQOZIUXNUDHUXQUDZVVOVXA
      VXAVVRVVOVXAXQVWTVVPHIUXQUXNUXJQWKXRXSVVOVWMVXAXIZVVRVVOUXQXELZUXNXELZUJZ
      VXBUXDVVMVXEUXGUXDVVMUJZVXCVXDVXFUXQUHLZVXCUXDVVKVXGVVLUXDVWQVXGVVKUXDVWQ
      UXPQOVXGUXEFXTZYAUXPCDEYBYCVWRYDVRUXQYEVFUXDVVLVXDVVKUXDVVLUJUXNUHLZVXDUX
      DUXLDXGZVXIVVLUXDVXJUXLQOVXIUXFGXTZYAUXLCDEYBYCUXFDXHZYDUXNYEVFVTYFWCHIUX
      QUXNYGVFVJYHYFVWJVWFYILZVWKVWNXIVXMVWJUXQUXNUXPUXMYJUXLUXMYJYKZYLVWFYIYMV
      FYHZVWGYNVFUOVVOVVRVWIVWEUCVWIVWJVWEVWIVWJDVWHUEZRZUYDVWHUYEUAZLZUYKVXRLZ
      UJZVVCUJZJXAAXAZUJZVWEVWIVWJUJZVXQVYCVWJVXQVWIVWJDUXQUEZUXNUEZXBZVXPVVODV
      YHRZVVRUXDVVMVYIUXGVXFVYHDDXBDVXFVYFDVYGDUXDVVKVYFDRZVVLUXDVWQVYJVVKUXPCD
      EYOVWRYDVRUXDVVLVYGDRZVVKUXDVXJVYKVVLUXLCDEYOVXLYDVTYPDYQYRWCVJVWJVWKVYHV
      XPRVXOVWGVYHVXMVYHVWGUEZRVXNVXMVWFUEVYLVYHVWFYIYSUXQUXNYTUUAUUBUUCVFUUDUU
      EVYEVXQVYCUXEVXRLZUXFVXRLZUXEUXFRZVGZVYCVYEVXQUJVYEVXQVYMVWJVWIVXQVYMUCZV
      VOVVRVWIVYQUCVVOVXQVWIVVRVYMVVOVWIVXQVVRVYMUCZUXDVVMUXGVWIVXQVYRUCUCUXDVX
      QVWIVVMUXGUJZVYRUXDVWIVXQVYSVYRUCUXDVWIVXQVYSVVRVYMUXDVWIVXQULZVYSVVRUJZU
      JZVYMVVKUXQVWHXGZUJZWUBVVKWUCVYSVVKVYTVVRVVKVVLUXGUUFUUGWUBUXQVWGVWHUXQVW
      GXGWUBUXQVWFVWGVWSVXMVWFVWGXGVXNVWFYIUUHUUBZUUIYLVYTWUAVWGVWHXGZUXDVWIWUA
      WUFUCVXQUXDVYSVVRWUFUXDVVMUXGVVRWUFUCVVOVVRWUFVWJVWKWUFVXOVWGUUJVFUOWRUUK
      XKUQZUULYFVYTVYMWUDXIWUAUXEVWHCDVXPEVXPUMZUUMVJYHUUNWPUUOUUPWPUUOUQUUQUQV
      YEVXQVYNVWJVWIVXQVYNUCZVVOVVRVWIWUIUCVVOVXQVWIVVRVYNVVOVWIVXQVVRVYNUCZUXD
      VVMUXGVWIVXQWUJUCUCUXDVXQVWIVYSWUJUXDVWIVXQVYSWUJUCUXDVWIVXQVYSVVRVYNWUBV
      YNVVLUXNVWHXGZUJZWUBVVLWUKVYSVVLVYTVVRVVKVVLUXGUURUUGWUBUXNVWGVWHUXNVWGXG
      WUBUXNVWFVWGUXNUXQUUSWUEUUIYLWUGUULYFVYTVYNWULXIWUAUXFVWHCDVXPEWUHUUMVJYH
      UUNWPUUOUUPWPUUOUQUUQUQVWJVYPVWIVXQVVOVYPVVRUXGUXDVYPVVMUXGVYPUXEUXFWKUUT
      UVAVJUVBVYBVYMVYNUJZVYPUJZAJUXEUXFVXHVXKUYDUXERZVYBVYMVXTUJZUXEUYKRZVGZUJ
      UYKUXFRZWUNWUOVYAWUPVVCWURWUOVXSVYMVXTUYDUXEVXRUVCUVDWUOUYNWUQUYDUXEUYKUV
      EUVFUVGWUSWUPWUMWURVYPWUSVXTVYNVYMUYKUXFVXRUVCUVHWUSWUQVYOUYKUXFUXEUVIUVF
      UVGUVJUVKUVLUOUVMVWDVYDBVWHUHUYAVWHRZUYCVXQVWCVYCWUTUYBVXPDUYAVWHUVNUVOWU
      TVWAVYBAJWUTUYMVYAVVCWUTUYGVXSUYLVXTWUTUYFVXRUYDUYAVWHUYEUVPZUVQWUTUYFVXR
      UYKWVAUVQUVGUVDUVRUVGUVSUVTUWAUOUWBVWEUYRVGZBUHUBVVSVWDWVBBUHVWDUYCUYQVGZ
      UJWVBVWCWVCUYCVWCUYPVGZAXAWVCVWBWVDAUYMUYNJUWCUWDUYPAUWEUWFUWGUYCUYQUWHUW
      FUWIUYRBUHUWJUWFUWKVVTUXOVGZHUXQUDVVRUXOHUXQUWLWVEVVQHUXQVVQWVEUXKIUXNUWL
      UWMUWNUWOUWPUWQWEWPUUOUWRUWSUYIUYRBUHUYHUYQUYCUYGUYLAJUYDUYKUYFUVCUWTUXAU
      WNUXBUXC $.
      $( [1-Dec-2010] $) $( [5-Sep-2009] $)
  $}


  $c FilMap $.
  $( Extend class definition to include the neighborhood filter mapping
     function. $)
  cfilmap $a class FilMap $.

  $c fLimf $.
  $( Extend class definition to include the function for filter-based function
     limits. $)
  cflimf $a class fLimf $.

  ${
    $d f s t w x y z $.
    $( Define a function that takes a filter to a neighborhood filter of the
       range.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    df-filmap $a |- FilMap = { <. <. x , y >. , z >. | ( y e. fBas
    /\ z = { <. f , s >. | ( f : U. y --> x /\ s = ( filGen ` ( { w | E. t e. y
    w = ( f " t ) } u. { x } ) ) ) } ) } $.
  $}

  ${
    $d f s A $.  $d f s t w x y z B $.  $d f s x y z X $.  $d f s x y z Y $.
    filmapf.1 $e |- Y = U. B $.
    $( Given a function from a filtered set to a topology, return the filter of
       supersets of images of filter elements.  (Contributed by Jeff Hankins,
       5-Sep-2009.) $)
    filmapf $p |- ( ( X e. A /\ B e. fBas ) -> ( X FilMap B ) = { <. f , s >.
    | ( f : Y --> X /\ s = ( filGen ` ( { w | E. t e. B w = ( f " t ) } u. { X
    } ) ) ) } ) $=
      ( vx vy vz wcel cfbas cv wf cima wceq wrex cab csn cun cfg cfv wa copab
      cvv cfilmap co cm wb elmapg cuni uniexg syl5eqel sylan2 anbi1d opabbidv
      oprex opabex2 syl6eqelr feq3 sneq uneq2d fveq2d eqeq2d anbi12d unieq
      syl6eqr feq2d rexeq abbidv uneq1d copab2 df-filmap visset biantrur anass
      bitr4i oprabbii eqtri oprabval2g elisset syl3an1 mpd3an3 ) FCMZDNMZGFEOZP
      ZHOZAOWHBOQRZBDSZATZFUAZUBZUCUDZRZUEZEHUFZUGMZFDUHUIWSRZWFWGUEZWSWHFGUJUI
      ZMZWQUEZEHUFUGXBXEWREHXBXDWIWQWFGUGMXDWIUKWGFGWHCUGULWGDUMZUGGDNUNIUOUPUQ
      UREHXCWPFGUJUSUTVAFUGMWGWTXAWFJKLFDUGNKOZUMZJOZWHPZWJWKBXGSZATZXIUAZUBZUC
      UDZRZUEZEHUFZWSUHXHFWHPZWJXLWNUBZUCUDZRZUEZEHUFUGXIFRZXQYCEHYDXJXSXPYBXIF
      XHWHVBYDXOYAWJYDXNXTUCYDXMWNXLXIFVCVDVEVFVGURXGDRZYCWREHYEXSWIYBWQYEXHGFW
      HYEXHXFGXGDVHIVIVJYEYAWPWJYEXTWOUCYEXLWMWNYEXKWLAWKBXGDVKVLVMVEVFVGURUHXG
      NMZLOXRRZUEZJKLVNXIUGMZYFUEYGUEZJKLVNJKLABEHVOYHYJJKLYHYIYHUEYJYIYHJVPVQY
      IYFYGVRVSVTWAWBFCWCWDWE $.
      $( [1-Dec-2010] $) $( [5-Sep-2009] $)
  $}

  ${
    $d f s A $.  $d f s t w B $.  $d f s t w F $.  $d f s X $.  $d f s Y $.
    isfilmap.1 $e |- Y = U. B $.
    $( Introduce a function that takes a function from a filtered domain to a
       set and produces a filter which consists of supersets of images of
       filter elements.  The functions which are dealt with by this function
       are similar to nets in topology.  For example, suppose we have a
       sequence filtered by the filter generated by its tails under the usual
       natural number ordering.  Then the elements of this filter are precisely
       the supersets of tails of this sequence.  Under this definition, it is
       not too difficult to see that the limit of a function in the filter
       sense captures the notion of convergence of a sequence.  As a result,
       the notion of a filter generalizes many ideas associated with sequences,
       and this function is one way to make that relationship precise in
       Metamath.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    isfilmap $p |- ( ( X e. A /\ B e. fBas /\ F : Y --> X ) -> ( ( X FilMap B
    ) ` F ) = ( filGen ` ( { w | E. t e. B w = ( F " t ) } u. { X } ) ) ) $=
      ( vf vs wcel cfbas wf w3a cfilmap co cfv cv cima wceq wrex cab csn cun
      cfg wa copab filmapf fveq1d 3adant3 wfun cop fvex funopfv wss pm3.27
      ssopab2i funopabeq funss mp2 a1i 3simp3 eqid jctir cm cvv wb feq1 imaeq1
      eqeq2d rexbidv abbidv uneq1d fveq2d anbi12d eqeq1 anbi2d opelopabg
      elmapg cuni uniexg syl5eqel sylan2 biimp3ar sylanc mpbird sylc eqtrd ) FC
      KZDLKZGFEMZNZEFDOPZQZEGFIRZMZJRZARZWOBRZSZTZBDUAZAUBZFUCZUDZUEQZTZUFZIJUG
      ZQZWREWSSZTZBDUAZAUBZXDUDZUEQZWIWJWNXJTWKWIWJUFEWMXIABCDIFGJHUHUIUJXIUKZE
      XPULXIKZXJXPTWLEXPXIXOUEUMZUNXQWLXIXGIJUGZUOXTUKXQXHXGIJWPXGUPUQIJXFURXIX
      TUSUTVAWLXRWKXPXPTZUFZWLWKYAWIWJWKVBXPVCVDEFGVEPZKZXPVFKZXRYBVGWLXHWKWQXP
      TZUFYBIJEXPYCVFWOETZWPWKXGYFGFWOEVHYGXFXPWQYGXEXOUEYGXCXNXDYGXBXMAYGXAXLB
      DYGWTXKWRWOEWSVIVJVKVLVMVNVJVOYFYFYAWKWQXPXPVPVQVRWIWJYDWKWIGVFKYDWKVGWJF
      GECVFVSWJDVTVFGDLWAHWBWCWDYEWLXSVAWEWFWGWH $.
      $( [1-Dec-2010] $) $( [5-Sep-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z F $.  $d x y z X $.
    $d x y z Y $.  $d x y z Z $.
    filmapss.1 $e |- Y = U. B $.
    filmapss.2 $e |- Z = U. C $.
    $( A finer filter produces a finer image filter.  (Contributed by Jeff
       Hankins, 16-Nov-2009.) $)
    filmapss $p |- ( ( ( X e. A /\ B e. fBas /\ C e. fBas ) /\ ( F : Y --> X /\
    Y = Z /\ B C_ C ) ) -> ( ( X FilMap B ) ` F ) C_ ( ( X FilMap C ) ` F ) )
        $=
      ( vx vy vz wcel cfbas w3a wf wceq wss wa cv cima wrex cab csn cun cfg
      cfv cfilmap co fgss cuni eqid extbas1 wfn filrn 3simp2 ffn 3ad2ant1
      syl2an wral wi sseq1 crn imassrn a1i frn sstrd syl5cbir a1d r19.23adv
      visset eqeq1 rexbidv elab syl5ib r19.21aiv unissb sylibr adantl sylanc
      3simp3 fneq2 biimpac sylan 3adant3 wal ssrexv 3ad2ant3 19.21aiv ss2ab
      unss1 syl syl3anc isfilmap 3expa 3ad2antr1 3adantl3 3simp1 adantr feq2
      3sstr4d ) EAMZBNMZCNMZOZFEDPZFGQZBCRZOZSZJTZDKTZUAZQZKBUBZJUCZEUDZUEZUFUG
      ZXNKCUBZJUCZXQUEZUFUGZDEBUHUIUGZDECUHUIUGZXRNMZYBNMZXRYBRZXSYCRXJXRYBUJXP
      NMZXPUKZERZYFXJEXPYJYJULUMXCDFUNZYIXEXIKJBXPDFHXPULUOXBXCXDUPXFXGYLXHFEDU
      QZURUSXIYKXEXFXGYKXHXFLTZERZLXPUTYKXFYOLXPXFYNXMQZKBUBZYOYNXPMXFYPYOKBXFY
      PYOVAZXLBMYPYOXMERXFYNXMEVBXFXMDVCZEXMYSRXFDXLVDVEFEDVFVGVHZVIVJXOYQJYNLV
      KZXKYNQZXNYPKBXKYNXMVLZVMVNVOVPLXPEVQVRURVSVTYANMZYAUKZERZYGXJEYAUUEUUEUL
      UMXDDGUNZUUDXEXIKJCYADGIYAULUOXBXCXDWAZXFXGUUGXHYLXGUUGXFXGYLUUGFGDWBWCYM
      WDWEUSXIUUFXEXFXGUUFXHXFYOLYAUTUUFXFYOLYAXFYPKCUBZYOYNYAMXFYPYOKCXFYRXLCM
      YTVIVJXTUUIJYNUUAUUBXNYPKCUUCVMVNVOVPLYAEVQVRURVSVTXJXPYARZYHXJXOXTVAZJWF
      UUJXJUUKJXIUUKXEXHXFUUKXGXNKBCWGWHVSWIXOXTJWJVRXPYAXQWKWLWMXBXCXIYDXSQZXD
      XBXCSXGXFUULXHXBXCXFUULJKABDEFHWNWOWPWQXBXDGEDPZYEYCQXJJKACDEGIWNXEXBXIXB
      XCXDWRWSXEXDXIUUHWSXIUUMXEXFXGUUMXHXGXFUUMFGEDWTWCWEVSWMXA $.
      $( [16-Nov-2009] $)
  $}

  ${
    $d t w A $.  $d t w B $.  $d t w F $.  $d t w X $.  $d t w Y $.
    fmf.1 $e |- Y = U. B $.
    $( A mapping filter is a filter.  (Contributed by Jeff Hankins,
       18-Sep-2009.) $)
    fmf $p |- ( ( X e. A /\ B e. fBas /\ F : Y --> X ) -> ( ( X FilMap B ) ` F
    ) e. Fil ) $=
      ( vw vt wcel cfbas wf w3a cfilmap co cfv cv cima wceq wrex cab csn cun
      cfg cfil isfilmap cuni wss eqid extbas1 wfn filrn ffn sylan2 3adant1
      ciun cvv wral wfun ffun visset funimaex syl 3ad2ant3 a1d r19.21aiv
      dfiun2g crn imassrn a1i frn sstrd iunss sylibr eqsstr3d sylanc fgfil
      eqeltrd ) DAIZBJIZEDCKZLZCDBMNOGPCHPZQZRHBSGTZDUAUBZUCOZUDGHABCDEFUEWAWEJ
      IZWFUDIWDJIZWDUFZDUGWGWADWDWIWIUHUIVSVTWHVRVSCEUJWHVTHGBWDCEFWDUHUKEDCULU
      MUNWAWIHBWCUOZDWAWCUPIZHBUQWJWIRWAWKHBWAWKWBBIZVTVRWKVSVTCURWKEDCUSCWBHUT
      VAVBVCVDVEHGBWCUPVFVBWAWCDUGZHBUQZWJDUGVTVRWNVSVTWMHBVTWMWLVTWCCVGZDWCWOU
      GVTCWBVHVIEDCVJVKVDVEVCHBWCDVLVMVNVOWEVPVBVQ $.
      $( [1-Dec-2010] $) $( [18-Sep-2009] $)
  $}

  ${
    $d t w A $.  $d t w B $.  $d t w F $.  $d t w X $.  $d t w Y $.
    fmbas.1 $e |- Y = U. B $.
    $( The base set of a mapping filter is the first argument.  (Contributed by
       Jeff Hankins, 18-Sep-2009.) $)
    fmbas $p |- ( ( X e. A /\ B e. fBas /\ F : Y --> X ) -> U. ( ( X FilMap B )
    ` F ) = X ) $=
      ( vw vt wcel cfbas wf w3a cfilmap co cfv cuni cv cima wceq wrex cab csn
      cun cfg isfilmap unieqd wss eqid extbas1 wfn filrn ffn sylan2 3adant1
      ciun cvv wral wfun ffun visset funimaex syl a1d r19.21aiv 3ad2ant3
      dfiun2g crn imassrn a1i frn sstrd iunss sylibr eqsstr3d sylanc fgbas
      extbas2 3simp1 3eqtr2d ) DAIZBJIZEDCKZLZCDBMNOZPGQCHQZRZSHBTGUAZDUBUCZUDO
      ZPZWHPZDWCWDWIGHABCDEFUEUFWCWHJIZWKWJSWGJIZWGPZDUGZWLWCDWGWNWNUHZUIWAWBWM
      VTWACEUJWMWBHGBWGCEFWGUHUKEDCULUMUNWCWNHBWFUOZDWCWFUPIZHBUQZWQWNSWBVTWSWA
      WBWRHBWBWRWEBIZWBCURWREDCUSCWEHUTVAVBVCVDVEHGBWFUPVFVBWCWFDUGZHBUQZWQDUGW
      BVTXBWAWBXAHBWBXAWTWBWFCVGZDWFXCUGWBCWEVHVIEDCVJVKVCVDVEHBWFDVLVMVNZVOWHW
      KWKUHVPVBWOVTWKDSWCDAWGWNWPVQXDVTWAWBVRVOVS $.
      $( [1-Dec-2010] $) $( [18-Sep-2009] $)
  $}

  ${
    $d t w x y z A $.  $d t w x y z B $.  $d t w x y C $.  $d t w x y z F $.
    $d x y z L $.  $d t w x y z X $.  $d t w x y z Y $.
    elfilmap.1 $e |- Y = U. B $.
    $( An element of a mapping filter.  (Contributed by Jeff Hankins,
       8-Sep-2009.) $)
    elfilmap $p |- ( ( X e. C /\ B e. fBas /\ F : Y --> X ) -> ( A e. ( ( X
    FilMap B ) ` F ) <-> ( A C_ X /\ E. x e. B ( F " x ) C_ A ) ) ) $=
      ( vw vt vy wcel cfbas wf w3a cfilmap co cfv cv cima wceq wrex cab csn
      cun cfg cuni wss wa isfilmap eleq2d wb eqid extbas1 wfn filrn ffn sylan2
      3adant1 ciun cvv wral wfun ffun visset funimaex syl 3ad2ant3 a1d
      r19.21aiv dfiun2g crn imassrn a1i frn sstrd iunss sylibr eqsstr3d sylanc
      elfg extbas2 3simp1 sseq2d wo wi imaeq2 sseq1d rcla4ev 3simp2 adantr
      sseq1 biimpa 3exp1 r19.23adv wex sstr2 3ad2ant2 sylbid imp 19.23adv 3imp
      c0 wne fbasne0 n0 sylib syl3an2 jaod elun eqeq1 rexbidv elab elsn
      orbi12i bitri syl5ib ssun1 eqeq2d mpan2 ad2antrl elabg 3syl mpbird
      sseldd simprr exp32 impbid anbi12d 3bitrd ) FDLZCMLZGFENZOZBEFCPQRZLBISZE
      JSZTZUAZJCUBZIUCZFUDZUEZUFRZLZBUUCUGZUHZKSZBUHZKUUCUBZUIZBFUHZEASZTZBUHZA
      CUBZUIYNYOUUDBIJDCEFGHUJUKYNUUCMLZUUEUUKULUUAMLZUUAUGZFUHZUUQYNFUUAUUSUUS
      UMZUNYLYMUURYKYLEGUOUURYMJICUUAEGHUUAUMUPGFEUQURUSYNUUSJCYRUTZFYNYRVALZJC
      VBUVBUUSUAYNUVCJCYNUVCYQCLZYMYKUVCYLYMEVCZUVCGFEVDZEYQJVEVFVGVHVIVJJICYRV
      AVKVGYNYRFUHZJCVBZUVBFUHYMYKUVHYLYMUVGJCYMUVGUVDYMYREVLZFYRUVIUHYMEYQVMVN
      GFEVOVPZVIVJVHJCYRFVQVRVSZVTKBUUCUUFUUFUMWAVGYNUUGUULUUJUUPYNUUFFBUUTYKUU
      FFUAYNFDUUAUUSUVAWBUVKYKYLYMWCVTWDYNUUJUUPYNUUIUUPKUUCYNUUHYRUAZJCUBZUUHF
      UAZWEZUUIUUPWFZUUHUUCLZYNUVMUVPUVNYNUVLUVPJCYMYKUVDUVLUVPWFWFYLYMUVDUVLUU
      IUUPUVDYRBUHZUUPYMUVDUVLOZUUIUIUUOUVRAYQCUUMYQUAUUNYRBUUMYQEWGWHWIZUVSUVD
      UUIYMUVDUVLWJWKUVSUUIUVRUVLYMUUIUVRULUVDUUHYRBWLVHWMVTWNVHWOYKUVDJWPZYMUV
      NUVPWFZYLYKUWAYMUWBYKUVDYMUWBWFZJUVDUWCWFYKUVDYMUVNUUIUUPUVDUVRUUPUVDYMUV
      NOZUUIUIUVTUWDUVDUUIUVDYMUVNWCWKUWDUUIUVRUWDUUIFBUHZUVRUVNUVDUUIUWEULYMUU
      HFBWLVHYMUVDUWEUVRWFZUVNYMUVGUWFUVJYRFBWQVGWRWSWTVTWNVNXAXBYLCXCXDUWACXEJ
      CXFXGXHXIUVQUUHUUALZUUHUUBLZWEUVOUUHUUAUUBXJUWGUVMUWHUVNYTUVMIUUHKVEYPUUH
      UAYSUVLJCYPUUHYRXKXLXMKFXNXOXPXQWOYNUUOUUJACYNUUMCLZUUOUUJUUNUUCLUUOUUJYN
      UWIUUOUIZUIZUUIUUOKUUNUUCUUHUUNBWLWIUWKUUAUUCUUNUUAUUCUHUWKUUAUUBXRVNUWKU
      UNUUALZUUNYRUAZJCUBZUWIUWNYNUUOUWIUUNUUNUAZUWNUUNUMUWMUWOJUUMCYQUUMUAYRUU
      NUUNYQUUMEWGXSWIXTYAYNUWLUWNULZUWJYMYKUWPYLYMUVEUUNVALUWPUVFEUUMAVEVFYTUW
      NIUUNVAYPUUNUAYSUWMJCYPUUNYRXKXLYBYCVHWKYDYEYNUWIUUOYFVTYGWOYHYIYJ $.
      $( [1-Dec-2010] $) $( [8-Sep-2009] $)

    elfilmap2.2 $e |- L = ( filGen ` B ) $.
    $( An element of a mapping filter.  (Contributed by Jeff Hankins,
       26-Sep-2009.) $)
    elfilmap2 $p |- ( ( X e. C /\ B e. fBas /\ F : Y --> X ) -> ( A e. ( ( X
    FilMap B ) ` F ) <-> ( A C_ X /\ E. x e. L ( F " x ) C_ A ) ) ) $=
      ( vy wcel cfbas wf w3a cfilmap co cfv wss cv cima wrex wa elfilmap wceq
      imaeq2 sseq1d rcla4ev cfg fbssfg syl6ssr sseld imp adantrr 3ad2antl2
      simprr sylanc exp32 r19.23adv wi wb elfg eleq2i syl5bb 3ad2ant2 sstr2
      com12 ad2antll imass2 syl5 reximdv expr com23 expimpd sylbid impbid
      anbi2d bitrd ) GDLZCMLZHGENZOZBEGCPQRLBGSZEKTZUAZBSZKCUBZUCWCEATZUAZBSZAF
      UBZUCKBCDEGHIUDWBWGWKWCWBWGWKWBWFWKKCWBWDCLZWFWKWDFLZWFWKWBWLWFUCZUCWJWFA
      WDFWHWDUEWIWEBWHWDEUFUGUHVTVSWNWMWAVTWLWMWFVTWLWMVTCFWDVTCCUIRZFCUJJUKULU
      MUNUOWBWLWFUPUQURUSWBWJWGAFWBWHFLZWHHSZWDWHSZKCUBZUCZWJWGUTZVTVSWPWTVAWAV
      TWHWOLWTWPKWHCHIVBFWOWHJVCVDVEWBWQWSXAWBWQUCWJWSWGWBWQWJWSWGUTWBWQWJUCUCZ
      WRWFKCXBWEWISZWFWRWJXCWFUTWBWQXCWJWFWEWIBVFVGVHWDWHEVIVJVKVLVMVNVOUSVPVQV
      R $.
      $( [1-Dec-2010] $) $( [26-Sep-2009] $)

    $( An alternate formulation of elementhood in a mapping filter that
       requires ` F ` to be onto.  (Contributed by Jeff Hankins,
       1-Oct-2009.) $)
    elfilmap3 $p |- ( ( B e. fBas /\ F : Y -onto-> X ) -> ( A e. ( ( X FilMap B
    ) ` F ) <-> E. x e. L A = ( F " x ) ) ) $=
      ( vy vz cfbas wcel wfo cvv cfilmap co cfv cv cima wceq wrex wb wa foima
      adantl wfun funimaexg fofun cuni uniexg syl5eqel adantr sylanc eqeltrrd
      w3a wss wf elfilmap2 fof syl3an3 ccnv imaeq2 eqeq2d rcla4ev cfil eqid
      fillsb cfg fgfil 3ad2ant2 ad2antrr simprl cdm cnvimass a1i wfn fofn fndm
      syl sseqtrd fgbas unieqi syl6eqr 3adant1 funimass3 3ad2ant3 elfg eleq2i
      syl5bb pm3.26bda sseq2 biimpar sylan 3ad2antl3 adantlr syldan biimpd
      impr 3jca sylc foimacnv eqcomd exp32 r19.23adv expimpd simprr crn
      imassrn forn eqsstrd sseq1d eqimss2 sylan2 jca impbid bitrd 3coml
      mpd3an3 ) CLMZGFDNZFOMZBDFCPQRMZBDASZTZUAZAEUBZUCZXTYAUDZDGTZFOYAYJFUAXTG
      FDUEUFDUGZGOMZYJOMYIDGOUHYAYKXTGFDUIZUFXTYLYAXTCUJOGCLUKHULUMUNUOYBXTYAYH
      YBXTYAUPZYCBFUQZDJSZTZBUQZJEUBZUDZYGYBXTGFDURYCYTUCYAJBCODEFGHIUSGFDUTVAY
      NYTYGYNYOYSYGYNYOUDZYRYGJEUUAYPEMZYRYGDVBBTZEMZBDUUCTZUAZYGUUAUUBYRUDZUDZ
      YFUUFAUUCEYDUUCUAYEUUEBYDUUCDVCVDVEEVFMZUUBUUCEUJZUQZYPUUCUQZUPUUDUUHYPUU
      CEUUJUUJVGVHYNUUIYOUUGXTYBUUIYAXTCVIRZVFECVJIULVKVLUUHUUBUUKUULUUAUUBYRVM
      YNUUKYOUUGXTYAUUKYBYIUUCGUUJYAUUCGUQXTYAUUCDVNZGUUCUUNUQYADBVOVPYADGVQUUN
      GUAZGFDVRGDVSVTZWAUFYIGUUMUJZUUJXTGUUQUAYACGHWBUMEUUMIWCWDWAWEVLUUAUUBYRU
      ULUUAUUBUDZYRUULYKYPUUNUQZYRUULUCUURYPBDWFYNYKYOUUBYAYBYKXTYMWGVLUUAUUBYP
      GUQZUUSUUAUUBUUTKSYPUQKCUBZUUAYPUUMMZUUTUVAUDZUUBYNUVBUVCUCZYOXTYBUVDYAKY
      PCGHWHVKUMEUUMYPIWIWJWKYNUUTUUSYOYAYBUUTUUSXTUUOUUTUUSYAUUOUUSUUTUUNGYPWL
      WMUUPWNWOWPWQUNWRWSWTXAUUAUUFUUGYAYBYOUUFXTYAYOUDUUEBGFBDXBXCWOUMUNXDXEXF
      YNYFYTAEYNYDEMZYFYTYNUVEYFUDZUDZYOYSUVGBYEFYNUVEYFXGUVGYEDXHZFYEUVHUQUVGD
      YDXIVPYNUVHFUAZUVFYAYBUVIXTGFDXJWGUMWAXKUVFYSYNUVEYEBUQZYSYFYRUVJJYDEYPYD
      UAYQYEBYPYDDVCXLVEYEBXMXNUFXOXDXEXPXQXRXS $.
      $( [1-Dec-2010] $) $( [1-Oct-2009] $)
  $}

  ${
    $d s x B $.  $d s x C $.  $d s x F $.  $d s x L $.  $d s x X $.
    $d s x Y $.
    fbfgfmeq.1 $e |- Y = U. B $.
    fbfgfmeq.2 $e |- L = ( filGen ` B ) $.
    $( The image filter of a filter base is the same as the image filter of its
       generated filter.  (Contributed by Jeff Hankins, 18-Nov-2009.) $)
    fbfgfmeq $p |- ( ( X e. C /\ B e. fBas /\ F : Y --> X ) -> ( ( X FilMap B )
    ` F ) = ( ( X FilMap L ) ` F ) ) $=
      ( vx vs wcel cfbas wf w3a cfilmap co cfv cv wss cima wrex wa elfilmap2
      cfg cuni wb unieqi eqcomi elfilmap 3simp1 cfil fgfil syl5eqel filfbas
      syl 3ad2ant2 fgbas feq2d adantl biimp3a syl3anc bitr4d eqrdv ) EBKZALKZFE
      CMZNZICEAOPQZCEDOPQZVGIRZVHKVJESCJRTVJSJDUAUBZVJVIKZJVJABCDEFGHUCVDDLKZAU
      DQZUEZECMZVLVKUFVGJVJDBCEVODUEVODVNHUGUHUIVDVEVFUJVEVDVMVFVEDUKKVMVEVNUKD
      AULHUMDUNUOUPVDVEVFVPVEVFVPUFVDVEFVOECAFGUQURUSUTVAVBVC $.
      $( [18-Nov-2009] $)
  $}

  ${
    $d f s x y z $.
    $( Define a function that gives the limits of a function ` f ` in the
       filter sense.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
    df-flimf $a |- fLimf = { <. <. x , y >. , z >. | ( x e. Top /\ y e. Fil /\
    z = { <. f , s >. | ( f : U. y --> U. x /\ s = ( ( fLim1 ` x ) ` ( ( U. x
    FilMap y ) ` f ) ) ) } ) } $.
  $}

  ${
    $d f j l s z F $.  $d f j l s z J $.  $d f j l s z X $.  $d f j l s z Y $.
    $d j l z f $.
    flimff.1 $e |- X = U. J $.
    flimff.2 $e |- Y = U. F $.
    $( Given a topology and a filtered set, return the convergence function on
       the functions from the filtered set to the base set of the topological
       space.  (Contributed by Jeff Hankins, 14-Oct-2009.) $)
    flimff $p |- ( ( J e. Top /\ F e. Fil ) -> ( J fLimf F ) = { <. f , s >. |
    ( f : Y --> X /\ s = ( ( fLim1 ` J ) ` ( ( X FilMap F ) ` f ) ) ) } ) $=
      ( vj vl vz ctop wcel cfil cv wf cfilmap co cfv cflim1 wceq wa copab cvv
      cflimf cm wb elmapg cuni uniexg syl5eqel syl2an anbi1d opabbidv oprex
      opabex2 syl6eqelr unieq syl6eqr feq3 syl fveq2 opreq1d fveq1d fveq12d
      eqeq2d anbi12d feq2d opreq2 fveq2d w3a copab2 df-flimf df-3an oprabbii
      eqtri oprabval2g mpd3an3 ) CLMZBNMZEDAOZPZFOZWADBQRZSZCTSZSZUAZUBZAFUCZUD
      MCBUERWJUAVSVTUBZWJWADEUFRZMZWHUBZAFUCUDWKWNWIAFWKWMWBWHDUDMEUDMWMWBUGVSV
      TDEWAUDUDUHVSCUIZUDDCLUJGUKVTBUIZUDEBNUJHUKULUMUNAFWLWGDEUFUOUPUQIJKCBLNJ
      OZUIZIOZUIZWAPZWCWAWTWQQRZSZWSTSZSZUAZUBZAFUCZWJUEWRDWAPZWCWADWQQRZSZWFSZ
      UAZUBZAFUCUDWSCUAZXGXNAFXOXAXIXFXMXOWTDUAXAXIUGXOWTWODWSCURGUSZWTDWRWAUTV
      AXOXEXLWCXOXCXKXDWFWSCTVBXOWAXBXJXOWTDWQQXPVCVDVEVFVGUNWQBUAZXNWIAFXQXIWB
      XMWHXQWREDWAXQWRWPEWQBURHUSVHXQXLWGWCXQXKWEWFXQWAXJWDWQBDQVIVDVJVFVGUNUEW
      SLMZWQNMZKOXHUAZVKZIJKVLXRXSUBXTUBZIJKVLIJKAFVMYAYBIJKXRXSXTVNVOVPVQVR $.
      $( [1-Dec-2010] $) $( [14-Oct-2009] $)
  $}

  ${
    $d f s F $.  $d f s J $.  $d f s L $.  $d f s X $.  $d f s Y $.
    sflimf.1 $e |- X = U. J $.
    sflimf.2 $e |- Y = U. L $.
    $( Given a function from a filtered set to a topological space, define the
       set of limit points of the function.  (Contributed by Jeff Hankins,
       8-Nov-2009.) $)
    sflimf $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) -> ( ( J fLimf L ) `
    F ) = ( ( fLim1 ` J ) ` ( ( X FilMap L ) ` F ) ) ) $=
      ( vf vs ctop wcel cfil wf w3a cflimf co cfv cv cfilmap cflim1 wceq wa
      copab flimff 3adant3 fveq1d wfun cop fvex funopfv wss pm3.27 ssopab2i
      funopabeq funss mp2 a1i 3simp3 eqid jctir cvv wb feq1 fveq2 fveq2d
      eqeq2d anbi12d eqeq1 anbi2d opelopabg fex cuni uniexg syl5eqel 3ad2ant2
      sylanc mpbird sylc eqtrd ) BJKZCLKZEDAMZNZABCOPZQAEDHRZMZIRZWEDCSPZQZBTQZ
      QZUAZUBZHIUCZQZAWHQZWJQZWCAWDWNVTWAWDWNUAWBHCBDEIFGUDUEUFWNUGZAWQUHWNKZWO
      WQUAWCAWQWNWPWJUIZUJWRWCWNWLHIUCZUKXAUGWRWMWLHIWFWLULUMHIWKUNWNXAUOUPUQWC
      WSWBWQWQUAZUBZWCWBXBVTWAWBURZWQUSUTAVAKZWQVAKZWSXCVBWCWMWBWGWQUAZUBXCHIAW
      QVAVAWEAUAZWFWBWLXGEDWEAVCXHWKWQWGXHWIWPWJWEAWHVDVEVFVGXGXGXBWBWGWQWQVHVI
      VJWBEVAKZXEWCEDVAAVKXDWAVTXIWBWACVLVAECLVMGVNVOVPXFWCWTUQVPVQVRVS $.
      $( [1-Dec-2010] $) $( [8-Nov-2009] $)
  $}

  ${
    $d n A $.  $d n s F $.  $d n J $.  $d n s L $.  $d n s X $.  $d n s Y $.
    flimfnei.1 $e |- X = U. J $.
    flimfnei.2 $e |- Y = U. L $.
    $( The property of being a limit point of a function in terms of
       neighborhoods.  (Contributed by Jeff Hankins, 9-Nov-2009.) $)
    flimfnei $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) -> ( A e. ( ( J
    fLimf L ) ` F ) <-> ( A e. X /\ A. n e. ( ( nei ` J ) ` { A } ) E. s e. L (
    F " s ) C_ n ) ) ) $=
      ( ctop wcel cfil wf w3a cflimf co cfv cfilmap cflim1 csn cnei wss wa cv
      cima wrex wral sflimf eleq2d cuni wceq wb eqid isfillim 3simp1 cvv cfbas
      fmf uniexg syl5eqel 3ad2ant1 filfbas 3ad2ant2 3simp3 syl3anc fmbas
      eqcomd neii1 3ad2antl1 elfilmap id syl3an adantr ibar adantl bitr4d
      syldan ralbidva dfss3 syl5bb anbi2d 3bitrd ) DKLZEMLZGFCNZOZACDEPQRZLACFE
      SQRZDTRRZLZAFLZAUAZDUBRRZWIUCZUDZWLCHUEUFBUEZUCHEUGZBWNUHZUDWGWHWJACDEFGI
      JUIUJWDWIMLZFWIUKZULWKWPUMWGAWIDFXAIXAUNUOWDWEWFUPFUQLZEURLZWFWTWGUQECFGJ
      USWDWEXBWFWDDUKUQFDKUTIVAZVBZWEWDXCWFEVCZVDZWDWEWFVEZVFWGXAFXBXCWFXAFULWG
      UQECFGJVGXEXGXHVFVHVFWGWOWSWLWGWQWILZBWNUHWSWOWGXIWRBWNWGWQWNLZWQFUCZXIWR
      UMWDWEXJXKWFWMDWQFIVIVJWGXKUDXIXKWRUDZWRWGXIXLUMZXKXBXCWFXMWDWEWFHWQEUQCF
      GJVKXDXFWFVLVMVNXKWRXLUMWGXKWRVOVPVQVRVSBWNWIVTWAWBWC $.
      $( [1-Dec-2010] $) $( [9-Nov-2009] $)
  $}

  ${
    $d n A $.  $d n s F $.  $d n J $.  $d n s L $.  $d n s N $.  $d n s X $.
    $d n s Y $.
    flimfneii.1 $e |- X = U. J $.
    flimfneii.2 $e |- Y = U. L $.
    $( A neighborhood of a limit point of a function contains the image of a
       filter element.  (Contributed by Jeff Hankins, 11-Nov-2009.) $)
    flimfneii $p |- ( ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) /\ A e. ( ( J
    fLimf L ) ` F ) /\ N e. ( ( nei ` J ) ` { A } ) ) -> E. s e. L ( F " s ) C_
    N ) $=
      ( vn ctop wcel cfil wf w3a cflimf co cfv csn cnei cv cima wss wrex wral
      flimfnei pm3.27bda 3adant3 wi wceq sseq2 rexbidv rcla4v 3ad2ant3 mpd ) CL
      MDNMGFBOPZABCDQRSMZEATCUASSZMZPBHUBUCZKUBZUDZHDUEZKUSUFZVAEUDZHDUEZUQURVE
      UTUQURAFMVEAKBCDFGHIJUGUHUIUTUQVEVGUJURVDVGKEUSVBEUKVCVFHDVBEVAULUMUNUOUP
      $.
      $( [1-Dec-2010] $) $( [11-Nov-2009] $)
  $}

  ${
    $d n o s A $.  $d n o s F $.  $d n o s J $.  $d n o s X $.  $d n o s Y $.
    flimopn.1 $e |- X = U. J $.
    flimopn.2 $e |- Y = U. F $.
    $( The condition for being a limit point of a filter still holds if one
       only considers open neighborhoods.  (Contributed by Jeff Hankins,
       4-Sep-2009.) $)
    flimopn $p |- ( ( ( J e. Top /\ F e. Fil /\ X = Y ) /\ A e. X ) -> ( A e. (
    ( fLim1 ` J ) ` F ) <-> A. o e. J ( A e. o -> o e. F ) ) ) $=
      ( vn vs ctop wcel cfil wceq w3a wa cflim1 cfv csn cnei wss cv wi wral wb
      flimelbas ex pm4.71rd isfillim bitr3d pm5.32 sylibr imp opnneip 3expib
      adantr ssel syl9 exp4a 3ad2antl1 r19.21adv wrex isnei snssi sylan2 eleq2
      eleq1 imbi12d rcla4v adantl snssg anbi1d ad2antlr pm2.27 sseq2 fillsb
      3expd com23 sylbid com24 3adant1 ad2antrr com13 syl6 com12 imp3a sylbird
      syld r19.23adv ssrdv impbid bitrd ) DKLZCMLZEFNZOZAELZPZACDQRRLZASZDTRRZC
      UAZABUBZLZXCCLZUCZBDUDZWPWQWSXBUEZWPWQWSPZWQXBPZUEWQXHUCWPWSXIXJWPWSWQWPW
      SWQACDEFGHUFUGUHACDEFGHUIUJWQWSXBUKULUMWRXBXGWRXBXFBDWMWNWQXBXCDLZXFUCUCW
      OWMWQPZXBXKXDXEXLXKXDPZXCXALZXBXEWMXMXNUCWQWMXKXDXNADXCUNUOUPXACXCUQURUSU
      TVAWRXGXBWRXGPZIXACXOIUBZXALZXPEUAZWTJUBZUAZXSXPUAZPZJDVBZPZXPCLZWRXQYDUE
      ZXGWMWNWQYFWOWMWTEUAYFWQWTJDXPEGVCAEVDVEUTUPXOXRYCYEXOYCXRYEXOYBXRYEUCZJD
      WRXGXSDLZYBYGUCZUCWRYHXGYIWRYHXGYIUCWRYHPZXGAXSLZXSCLZUCZYIYHXGYMUCWRXFYM
      BXSDXCXSNXDYKXEYLXCXSAVFXCXSCVGVHVIVJYJYBYMYGYJYBYKYAPZYMYGUCZWQYNYBUEWPY
      HWQYKXTYAAXSEVKVLVMYJYKYAYOYKYJYAYOUCYKYMYAYJYGYKYMYLYAYJYGUCUCYKYLVNYJYA
      YLYGWPYAYLYGUCUCZWQYHWNWOYPWMWNWOPZXRYLYAYEYQXRXPFUAZYLYAYEUCZUCZWOXRYRUE
      WNEFXPVOVJWNYRYTUCWOWNYLYRYSWNYLYRYAYEXSXPCFHVPVQVRUPVSVTWAWBWCWDVTWEWFWG
      VRWHUGVRUMWIVRWFVSWJUGWKWL $.
      $( [1-Dec-2010] $) $( [4-Sep-2009] $)
  $}

  ${
    $d o A $.  $d o x B $.  $d o F $.  $d o J $.  $d o X $.  $d o Y $.
    fbaslim.1 $e |- X = U. J $.
    fbaslim.2 $e |- Y = U. B $.
    fbaslim.3 $e |- F = ( filGen ` B ) $.
    $( A condition for a filter to converge to a point involving one of its
       bases.  (Contributed by Jeff Hankins, 4-Sep-2009.) $)
    fbaslim $p |- ( ( ( J e. Top /\ B e. fBas /\ X = Y ) /\ A e. X ) -> ( A e.
    ( ( fLim1 ` J ) ` F ) <-> A. o e. J ( A e. o -> E. x e. B x C_ o ) ) ) $=
      ( ctop wcel cfbas wceq w3a wa cflim1 cfv cv wi wral wss wrex cfil cuni
      wb eqid flimopn 3simp1 cfg fgfil syl5eqel 3ad2ant2 3simp3 fgbas unieqi
      syl6eqr eqtrd 3jca sylan eltopss ex 3ad2ant1 adantr sseq2 3ad2ant3
      biimpa elfg eleq2i syl5bb ibar adantl bitr4d 3ad2antl2 syldan imbi2d
      syld pm5.74d ralbidv2 bitrd ) FLMZCNMZGHOZPZBGMZQZBEFRSSMZBDTZMZWIEMZUAZD
      FUBZWJATWIUCACUDZUAZDFUBWBEUEMZGEUFZOZPWFWHWMUGWEBDEFGWQIWQUHUIWEWBWPWRWB
      WCWDUJWCWBWPWDWCCUKSZUEECULKUMUNWEGHWQWBWCWDUOWCWBHWQOWDWCHWSUFWQCHJUPEWS
      KUQURUNUSUTVAWGWLWODFFWGWIFMZWLWOWGWTWIGUCZWLWOUGZWEWTXAUAZWFWBWCXCWDWBWT
      XAWIFGIVBVCVDVEWEXAXBUAWFWEXAXBWEXAQWKWNWJWEXAWIHUCZWKWNUGZWEXAXDWDWBXAXD
      UGWCGHWIVFVGVHWCWBXDXEWDWCXDQWKXDWNQZWNWCWKXFUGXDWCWIWSMXFWKAWICHJVIEWSWI
      KVJVKVEXDWNXFUGWCXDWNVLVMVNVOVPVQVCVEVRVSVTWA $.
      $( [4-Sep-2009] $)
  $}

  ${
    $d o A $.  $d o s F $.  $d o J $.  $d o s L $.  $d o s X $.  $d o s Y $.
    isflimf.1 $e |- X = U. J $.
    isflimf.2 $e |- Y = U. L $.
    $( The property of being a limit point of a function.  (Contributed by Jeff
       Hankins, 8-Nov-2009.) $)
    isflimf $p |- ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) -> ( A e. ( ( J
    fLimf L ) ` F ) <-> ( A e. X /\ A. o e. J ( A e. o -> E. s e. L ( F " s )
    C_ o ) ) ) ) $=
      ( ctop wcel cfil wf w3a cflimf co cfv cfilmap cflim1 cv wi wral wa cima
      wss wrex sflimf eleq2d pm3.26 cuni wceq eqid flimelbas 3simp1 cvv cfbas
      fmf uniexg syl5eqel 3ad2ant1 filfbas 3ad2ant2 3simp3 syl3anc fmbas
      eqcomd 3jca sylan jca anim2i wb flimopn ibar adantl bitrd pm5.21nd
      elfilmap adantr elssuni syl6ssr biantrurd bitr4d imbi2d ralbidva anbi2d
      3bitrd ) DKLZEMLZGFCNZOZACDEPQRZLACFESQRZDTRRZLZAFLZABUAZLZWQWMLZUBZBDUCZ
      UDZWPWRCHUAUEWQUFHEUGZUBZBDUCZUDWKWLWNACDEFGIJUHUIWKWOXBWKWPUDZWKWOUDWKWP
      WKWOUJWHWMMLZFWMUKZULZOZWOWPWKAWMDFXHIXHUMZUNWKWHXGXIWHWIWJUOFUPLZEUQLZWJ
      XGWKUPECFGJURWHWIXLWJWHDUKZUPFDKUSIUTVAZWIWHXMWJEVBVCZWHWIWJVDZVEXLXMWJXI
      WKXLXMWJOXHFUPECFGJVFVGXOXPXQVEVHZVIVJXBWPWKWPXAUJVKXFWOXAXBXJWPWOXAVLWKA
      BWMDFXHIXKVMXRVIWPXAXBVLWKWPXAVNVOVPVQWKXAXEWPWKWTXDBDWKWQDLZUDZWSXCWRXTW
      SWQFUFZXCUDZXCWKWSYBVLZXSXLXMWJYCWKHWQEUPCFGJVRXOXPXQVEVSXSXCYBVLWKXSYAXC
      XSWQXNFWQDVTIWAWBVOWCWDWEWFWG $.
      $( [1-Dec-2010] $) $( [8-Nov-2009] $)
  $}

  ${
    $d o s A $.  $d o s F $.  $d o s J $.  $d o s L $.  $d o s X $.
    $d o s Y $.
    flimfelbas.1 $e |- X = U. J $.
    flimfelbas.2 $e |- Y = U. L $.
    $( A limit point of a function is in the topological space.  (Contributed
       by Jeff Hankins, 10-Nov-2009.) $)
    flimfelbas $p |- ( ( ( J e. Top /\ L e. Fil /\ F : Y --> X ) /\ A e. ( ( J
    fLimf L ) ` F ) ) -> A e. X ) $=
      ( vo vs ctop wcel cfil wf w3a cflimf co cfv cv cima wss wrex wi wral
      isflimf pm3.26bda ) CKLDMLFEBNOABCDPQRLAELAISZLBJSTUGUAJDUBUCICUDAIBCDEFJ
      GHUEUF $.
      $( [1-Dec-2010] $) $( [10-Nov-2009] $)
  $}


  ${
    $d F f x $.  $d J f x $.  $d X f x $.
    hausfillim2.1 $e |- X = U. J $.
    $( A condition for a topology to be Hausdorff in terms of filters.  A
       topology is Hausdorff iff every filter has at most one limit point.
       (Contributed by FL, 6-Dec-2010.) $)
    hausfillim2 $p |- ( ( J e. Haus /\ F e. Fil /\ X = U. F ) -> E*
    x x e. ( ( fLim1 ` J ) ` F ) ) $=
      ( vf cha wcel cfil cuni wceq cv cflim1 cfv wmo ctop wi haustop wral
      hausfillim unieq eqeq2d ax-17 fveq2 eleq2d mobid imbi12d rcla4cv syl6bi
      mpcom 3imp ) CGHZBIHZDBJZKZALZBCMNZNZHZAOZCPHZULUMUOUTQZQZCRVAULDFLZJZKZU
      PVDUQNZHZAOZQZFISVCAFCDETVJVBFBIVDBKZVFUOVIUTVKVEUNDVDBUAUBVKVHUSAVKAUCVK
      VGURUPVDBUQUDUEUFUGUHUIUJUK $.
      $( [1-Dec-2010] $) $( [14-Nov-2010] $)
  $}

  ${

    holimf.1 $e |- Y = U. L $.
    holimf.2 $e |- X = U. J $.
    ${
      $d F x $.  $d J x $.  $d L x $.  $d X x $.  $d Y x $.
      $( If a function has its values in a Hausdorff space then it has at most
         one limit value.  (Contributed by FL, 6-Dec-2010.) $)
      holimf $p |- ( ( L e. Fil /\ J e. Haus /\ F : Y --> X )
       -> E* x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cha wf w3a cv cflimf co cfv wmo cfilmap cflim1 cuni wceq
        hausfillim2 3simp2 cvv cfbas fmf uniexg syl5eqel 3ad2ant2 filfbas
        3ad2ant1 3simp3 syl3anc fmbas eqcomd ax-17 ctop sflimf haustop 3simp1
        eleq2d mobid mpbird ) DIJZCKJZFEBLZMZANZBCDOPQZJZARVIBEDSPQZCTQQZJZARZV
        FVLIJZEVLUAZUBVOVHAVLCEHUCVEVFVGUDEUEJZDUFJZVGVPVHUEDBEFGUGVFVEVRVGVFCU
        AUEECKUHHUIUJZVEVFVSVGDUKULZVEVFVGUMZUNVHVQEVRVSVGVQEUBVHUEDBEFGUOVTWAW
        BUNUPUNVHVKVNAVHAUQVHVJVMVICURJZVEVGVJVMUBVHBCDEFHGUSVFVEWCVGCUTUJVEVFV
        GVAWBUNVBVCVD $.
        $( [1-Dec-2010] $) $( [14-Nov-2010] $)
    $}

    ${
      $d F x $.  $d J x $.  $d L x $.  $d X x $.  $d Y x $.
      $( If a convergent function has its values in a Hausdorff space then it
         has only one limit value.  (Contributed by FL, 6-Dec-2010.) $)
      holimf2 $p |- ( ( ( L e. Fil /\ J e. Haus /\ F : Y --> X ) /\
                   ( ( J fLimf L ) ` F ) =/= (/) )
       -> E! x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cha wf w3a cflimf co cfv c0 wne wa cv wmo weu holimf
        adantr wb n0moeu adantl mpbid ) DIJCKJFEBLMZBCDNOPZQRZSATUJJZAUAZULAUBZ
        UIUMUKABCDEFGHUCUDUKUMUNUEUIAUJUFUGUH $.
        $( [1-Dec-2010] $) $( [14-Nov-2010] $)
    $}
  $}


