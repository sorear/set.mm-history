$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Finite intersections
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c fi $.
  $( Extend class notation with the function whose value is the class of all
     the finite intersections of the elements of a given set. $)
  cfi $a class fi $.

  ${
    $d x y z $.
    $( Function whose value is the class of all the finite intersections of the
       elements of ` x ` .  (Contributed by FL, 2-Sep-2008.) $)
    df-fi $a |- fi = ( x e. _V |-> { z |
                  E. y e. ( ~P x i^i Fin ) z = |^| y } ) $.
  $}

  ${
    $d A x y z $.  $d B x y $.
    $( The set of all the finite intersections of the elements of ` A ` .
       (Contributed by FL, 2-Sep-2008.) $)
    fival $p |- ( A e. V -> ( fi ` A ) = { y |
                  E. x e. ( ~P A i^i Fin ) y = |^| x } ) $=
      ( vz wcel cvv cv cint wceq cpw cfn cin wrex cab cfi cfv elisset cuni wss
      wa simpr c0 wne inss1 sseli elpwi syl visset eleq1 mpbii intex sylibr
      intssuni2 syl2an eqsstrd elpw r19.23aiva abssi uniexg pwexg ssexg
      sylancr pweq ineq1d rexeqdv abbidv df-fi fvmptg syl11anc ) CDFZCGFBHZAHZI
      ZJZACKZLMZNZBOZGFZCPQVSJCDRVKVSCSZKZTWBGFZVTVRBWBVOVLWBFZAVQVMVQFZVOUAZVL
      WATWDWFVLVNWAWEVOUBWEVMCTZVMUCUDZVNWATVOWEVMVPFWGVQVPVMVPLUEUFVMCUGUHVOVN
      GFZWHVOVLGFWIBUIZVLVNGUJUKVMULUMVMCUNUOUPVLWAWJUQUMURUSVKWAGFWCCDUTWAGVAU
      HVSWBGVBVCECVOAEHZKZLMZNZBOVSGGPWKCJZWNVRBWOVOAWMVQWOWLVPLWKCVDVEVFVGEABV
      HVIVJ $.
      $( [24-Nov-2013] $) $( [27-Apr-2008] $)

    $( Specific properties of an element of ` ( fi `` B ) ` .  (Contributed by
       FL, 2-Sep-2008.) $)
    elfi $p |- ( ( A e. V /\ B e. W ) -> ( A e. ( fi ` B ) <->
        E. x e. ( ~P B i^i Fin ) A = |^| x ) ) $=
      ( vy wcel cfi cfv cv cint wceq cpw cfn cin wrex cab fival eleq2d eqeq1
      rexbidv elabg sylan9bbr ) CEGZBCHIZGBFJZAJKZLZACMNOZPZFQZGBDGBUGLZAUIPZUD
      UEUKBAFCERSUJUMFBDUFBLUHULAUIUFBUGTUAUBUC $.
      $( [24-Nov-2013] $) $( [27-Apr-2008] $)

    $( Sufficient condition for an element of ` ( fi `` B ) ` .  (Contributed
       by Mario Carneiro, 24-Nov-2013.) $)
    elfir $p |- ( ( B e. V /\ ( A C_ B /\ A =/= (/) /\ A e. Fin ) ) ->
        |^| A e. ( fi ` B ) ) $=
      ( vx wcel wss c0 wne cfn w3a wa cint cfi cfv cv wceq cpw cin wrex simp1
      elpw2g syl5ibr imp simpr3 elin sylanbrc eqid inteq eqeq2d rcla4ev
      sylancl cvv wb simp2 intex sylib id elfi syl2anc mpbird ) BCEZABFZAGHZAIE
      ZJZKZALZBMNEZVGDOZLZPZDBQZIRZSZVFAVMEZVGVGPZVNVFAVLEZVDVOVAVEVQVEVQVAVBVB
      VCVDTABCUAUBUCVAVBVCVDUDAVLIUEUFVGUGVKVPDAVMVIAPVJVGVGVIAUHUIUJUKVEVGULEZ
      VAVHVNUMVAVEVCVRVBVCVDUNAUOUPVAUQDVGBULCURUSUT $.
      $( [24-Nov-2013] $)
  $}

  ${
    $d A x y $.  $d x V $.
    $( Any element of a set ` A ` is the intersection of a finite subset of
       ` A ` .  (Contributed by FL, 27-Apr-2008.) $)
    ssfii $p |- ( A e. V -> A C_ ( fi ` A ) ) $=
      ( vx vy wcel cfi cfv cv cint wceq cpw cfn cin wrex csn snelpwi snfi a1i
      elin sylanbrc visset intsn eqcomi inteq eqeq2d rcla4ev sylancl cvv wb
      elfi mpan syl5ibr ssrdv ) ABEZCAAFGZCHZAEZUPUOEZUNUPDHZIZJZDAKZLMZNZUQUPO
      ZVCEZUPVEIZJZVDUQVEVBEVELEZVFUPAPVIUQUPQRVEVBLSTVGUPUPCUAZUBUCVAVHDVEVCUS
      VEJUTVGUPUSVEUDUEUFUGUPUHEUNURVDUIVJDUPAUHBUJUKULUM $.
      $( [24-Nov-2013] $) $( [27-Apr-2008] $)

    $( If ` A ` is not empty, the class of all the finite intersections of
       ` A ` is not empty either.  (Contributed by FL, 2-Sep-2008.) $)
    fi0 $p |- ( A e. V -> ( A = (/) <-> ( fi ` A ) = (/) ) ) $=
      ( vy vx wcel c0 wceq cfi cfv cv cint cpw cfn cin wrex cab cvv 0ex fival
      ax-mp wex wn wss inss1 sseli elpwi ss0 3syl wne intex isset bitri
      necon1bbii sylibr nrex rexcom4 mtbi abn0 mpbi eqtri fveq2 eqeq1d mpbiri
      ssfii sseq0 sylan ex impbid2 ) ABEZAFGZAHIZFGZVJVLFHIZFGVMCJDJZKZGZDFLZMN
      ZOZCPZFFQEVMVTGRDCFQSTVSCUAZUBVTFGVPCUAZDVROWAWBDVRVNVREZVNFGZWBUBWCVNVQE
      VNFUCWDVRVQVNVQMUDUEVNFUFVNUGUHWBVNFVNFUIVOQEWBVNUJCVOUKULUMUNUOVPDCVRUPU
      QWAVTFVSCURUMUSUTVJVKVMFAFHVAVBVCVIVLVJVIAVKUCVLVJABVDAVKVEVFVGVH $.
      $( [24-Nov-2013] $) $( [27-Apr-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    $( The elements of ` ( fi `` C ) ` are closed under finite intersection. $)
    fiin $p |- ( ( A e. ( fi ` C ) /\ B e. ( fi ` C ) ) ->
                 ( A i^i B ) e. ( fi ` C ) ) $=
      ( vz vx vy cfi cfv wcel wa cin cv cint wceq cpw cfn wrex cvv wb c0 wne
      ne0i fvprc necon1ai syl elfi mpdan ibi adantr simpr ancoms sylan mpbid
      wi cun wss elpwi anim12i unss sylib visset unex elpw sylibr unfi an4s
      elin anbi12i 3imtr4i ineq12 intun syl6eqr inteq eqeq2d rcla4ev syl2an
      expr r19.23adva r19.23aiva imp syl11anc inex1g mpbird ) ACGHZIZBWDIZJZABK
      ZWDIZWHDLZMZNZDCOZPKZQZWGAELZMZNZEWNQZBFLZMZNZFWNQZWOWEWSWFWEWSWECRIZWEWS
      SWEWDTUAXDWDAUBXDWDTCGUCUDUEZEACWDRUFUGUHUIWGWFXCWEWFUJWEXDWFWFXCSZXEWFXD
      XFFBCWDRUFUKULUMWSXCWOWRXCWOUNEWNWPWNIZWRJZXBWOFWNXHWTWNIZXBWOXGXIWRXBWOX
      GXIJZWPWTUOZWNIZWHXKMZNZWOWRXBJZWPWMIZWPPIZJZWTWMIZWTPIZJZJXKWMIZXKPIZJZX
      JXLXPXSXQXTYDXPXSJZYBXQXTJYCYEXKCUPZYBYEWPCUPZWTCUPZJYFXPYGXSYHWPCUQWTCUQ
      URWPWTCUSUTXKCWPWTEVAFVAVBVCVDWPWTVEURVFXGXRXIYAWPWMPVGWTWMPVGVHXKWMPVGVI
      XOWHWQXAKXMAWQBXAVJWPWTVKVLWLXNDXKWNWJXKNWKXMWHWJXKVMVNVOVPVFVQVRVSVTWAWE
      WIWOSZWFWEWHRIXDYIABWDWBXEDWHCRRUFWAUIWC $.
      $( [24-Nov-2013] $)
  $}

  ${
    $d t x y z A $.
    $( The set of finite intersections is the smallest set that contains ` A `
       and is closed under pairwise intersection. $)
    dffi2 $p |- ( A e. V -> ( fi ` A ) =
        |^| { z | ( A C_ z /\ A. x e. z A. y e. z ( x i^i y ) e. z ) } ) $=
      ( vt wcel cvv cfi cfv cv wss cin wral wa cab cint wceq elisset wi wal
      cpw cfn wrex wb visset elfi mpan biimpd wex df-rex c0 wne w3a fiint
      inss1 sseli elpwi syl 3ad2ant2 simp1 sstrd eleq1 mpbii intex sylibr
      3ad2ant3 inss2 3jca 3expib pm2.27 syl6 biimprd adantl a1i syldd com23
      alimdv syl5bi imp 19.23v sylib sylan9 ssrdv ex 19.21aiv ssintab ssfii
      fiin rgen2 fvex sseq2 eleq2 raleqbi1dv anbi12d elab sylanbrc intss1
      eqssd ) DEGDHGZDIJZDCKZLZAKZBKZMZXBGZBXBNZAXBNZOZCPZQZRDESWTXAXLWTXJXAXBL
      ZTZCUAXAXLLWTXNCWTXJXMWTXJOFXAXBWTFKZXAGZXOXDQZRZADUBZUCMZUDZXJXOXBGZWTXP
      YAXOHGZWTXPYAUEFUFZAXODHHUGUHUIYAXDXTGZXROZAUJZXJYBXRAXTUKXJYFYBTZAUAZYGY
      BTXCXIYIXIXDXBLZXDULUMZXDUCGZUNZXQXBGZTZAUAXCYIABXBUOXCYOYHAXCYFYOYBXCYFY
      OYNYBXCYFYMYOYNTXCYEXRYMXCYEXRUNZYJYKYLYPXDDXBYEXCXDDLZXRYEXDXSGYQXTXSXDX
      SUCUPUQXDDURUSUTXCYEXRVAVBXRXCYKYEXRXQHGZYKXRYCYRYDXOXQHVCVDXDVEVFVGYEXCY
      LXRXTUCXDXSUCVHUQUTVIVJYMYNVKVLYFYNYBTZTXCXRYSYEXRYBYNXOXQXBVCVMVNVOVPVQV
      RVSVTYFYBAWAWBVSWCWDWEWFXJCXAWGVFWTXAXKGZXLXALWTDXALZXFXAGZBXANZAXANZYTDH
      WHUUDWTUUBABXAXAXDXEDWIWJVOXJUUAUUDOCXADIWKXBXARXCUUAXIUUDXBXADWLXHUUCAXB
      XAXGUUBBXBXAXBXAXFWMWNWNWOWPWQXAXKWRUSWSUS $.
      $( [24-Nov-2013] $)

    $( The set of finite intersections of a set is contained in the powerset of
       the union of the elements of ` A ` . $)
    fipwuni $p |- ( A e. V -> ( fi ` A ) C_ ~P U. A ) $=
      ( vz vx vy wcel cfi cfv cv wss cin wral wa cab cint cuni cpw dffi2 pwuni
      ssinss1 visset elpw inex1 3imtr4i adantr rgen2 pm3.2i cvv wb uniexg
      pwexg wceq sseq2 eleq2 raleqbi1dv anbi12d elabg 3syl mpbiri intss1 syl
      eqsstrd ) ABFZAGHACIZJZDIZEIZKZVDFZEVDLZDVDLZMZCNZOZAPZQZDECABRVCVPVMFZVN
      VPJVCVQAVPJZVHVPFZEVPLZDVPLZMZVRWAASVSDEVPVPVFVPFZVSVGVPFVFVOJVHVOJWCVSVF
      VGVOTVFVODUAZUBVHVOVFVGWDUCUBUDUEUFUGVCVOUHFVPUHFVQWBUIABUJVOUHUKVLWBCVPU
      HVDVPULVEVRVKWAVDVPAUMVJVTDVDVPVIVSEVDVPVDVPVHUNUOUOUPUQURUSVPVMUTVAVB $.
      $( [24-Nov-2013] $)

    $( A set which is closed under pairwise intersection is closed under finite
       intersection.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    inficl $p |- ( A e. V ->
        ( A. x e. A A. y e. A ( x i^i y ) e. A <-> ( fi ` A ) = A ) ) $=
      ( vz wcel cv cin wral cfi cfv wceq wss wa cab cint eqimss2 biantrurd
      eleq2 raleqbi1dv bitr3d elabg intss1 syl6bir dffi2 sseq1d sylibrd wi
      ssfii eqss biimpri expcom syl syld fiin rgen2 mpbii impbid1 ) CDFZAGZBGZH
      ZCFZBCIZACIZCJKZCLZUSVEVFCMZVGUSVECEGZMZVBVIFZBVIIZAVIIZNZEOZPZCMZVHUSVEC
      VOFVQVNVEECDVICLZVMVNVEVRVJVMCVIQRVLVDAVICVKVCBVICVICVBSTTUAUBCVOUCUDUSVF
      VPCABECDUEUFUGUSCVFMZVHVGUHCDUIVHVSVGVGVHVSNVFCUJUKULUMUNVGVBVFFZBVFIZAVF
      IVEVTABVFVFUTVACUOUPWAVDAVFCVTVCBVFCVFCVBSTTUQUR $.
      $( [24-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x y J $.
    $( A topology is closed under finite intersections.  (Contributed by Jeff
       Hankins, 7-Oct-2009.) $)
    fitop $p |- ( J e. Top -> ( fi ` J ) = J ) $=
      ( vx vy ctop wcel cv cin wral cfi cfv wceq inopn 3expib r19.21aivv
      inficl mpbid ) ADEZBFZCFZGAEZCAHBAHAIJAKQTBCAAQRAESAETRSALMNBCADOP $.
      $( [7-Oct-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d y V $.
    $( Subset relationship for function ` fi ` .  (Contributed by Jeff Hankins,
       7-Oct-2009.) $)
    fiss $p |- ( ( B e. V /\ A C_ B ) -> ( fi ` A ) C_ ( fi ` B ) ) $=
      ( vy vx vz wcel wss wa cv cin wral cab cint cfi cfv wi sstr2 adantl
      anim1d ss2abdv intss syl cvv wceq ssexg ancoms dffi2 adantr 3sstr4d ) BCG
      ZABHZIZADJZHZEJFJKUNGFUNLEUNLZIZDMZNZBUNHZUPIZDMZNZAOPZBOPZUMVBURHUSVCHUM
      VAUQDUMUTUOUPULUTUOQUKABUNRSTUAVBURUBUCUMAUDGZVDUSUEULUKVFABCUFUGEFDAUDUH
      UCUKVEVCUEULEFDBCUHUIUJ $.
      $( [24-Nov-2013] $) $( [7-Oct-2009] $)
  $}

  $( The union of the finite intersections of a set is simply the union of the
     set itself.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
  fiuni $p |- ( A e. V -> U. A = U. ( fi ` A ) ) $=
    ( wcel cuni cfi cfv wss ssfii uniss syl cpw fipwuni unipw syl6sseq eqssd )
    ABCZADZAEFZDZPARGQSGABHARIJPSQKZDZQPRTGSUAGABLRTIJQMNO $.
    $( [24-Nov-2013] $) $( [5-Sep-2009] $)

  ${
    $d x y A $.
    $( A collection of finite intersections is a basis.  The initial set is a
       subbasis for the topology.  Compare ~ subbas .  (Contributed by Jeff
       Hankins, 25-Aug-2009.) $)
    fibas $p |- ( fi ` A ) e. TopBases $=
      ( vx vy cfi cfv cvv wcel cv cin wral ctb fvex fiin rgen2 fiinbas mp2an )
      ADEZFGBHZCHZIQGZCQJBQJQKGADLTBCQQRSAMNBCQFOP $.
      $( [24-Nov-2013] $) $( [25-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.  $d x y z K $.
    $( A finite intersection of elements taken from a union of collections.
       (Contributed by Jeff Hankins, 15-Nov-2009.)  (The proof was shortened by
       Mario Carneiro, 26-Nov-2013.) $)
    elfiun $p |- ( ( B e. D /\ C e. K ) -> ( A e. ( fi ` ( B u. C ) ) <-> ( A
    e. ( fi ` B ) \/ A e. ( fi ` C ) \/ E. x e. ( fi ` B ) E. y e. ( fi ` C ) A
    = ( x i^i y ) ) ) ) $=
      ( vz wcel wa cun cfi cfv cv cin wceq wrex w3o cvv w3a elisset adantl
      simpll simplr 3jca wi 3anim1i 3expib visset inex1 eleq1 mpbiri a1i
      r19.23aivv 3jaoi impcom cint cpw cfn wb simp1 unexg 3adant1 elfi
      syl11anc c0 wne simpl1 intex syl6bbr biimpcd syl wn nne wss simp23 cdif
      simp3l inss1 sseli elpwi reldisj mpbid uncom difeq1i difun2 eqtri difss
      eqsstri syl6ss simp3r inss2 elfir syl13anc 3mix2 sylbi simp22 3mix1
      simp1l ssfi mpan2 3syl simp1r indi df-ss biimpi syl5reqr inteqd intun
      syl6eq ineq1 eqeq2d ineq2 rcla42ev syl111anc 3mix3 ecase eqeq1 2rexbidv
      3orbi123d syl5ibrcom expr com23 mpdd r19.23adva sylbid ssun1 fiss
      sylancl sseld ssun2 anim12d fiin eleq1a syl6 r19.23advv 3jaod impbid
      pm5.21nd ) DFIZEGIZJZCDEKZLMZIZCDLMZIZCELMZIZCANZBNZOZPZBUUHQAUUFQZRZCSIZ
      YTUUATZUUBUUEJUUPYTUUAUUEUUPUUBCUUDUAUBYTUUAUUEUCYTUUAUUEUDUEUUOUUBUUQUUG
      UUBUUQUFUUIUUNUUGYTUUAUUQUUGUUPYTUUACUUFUAUGUHUUIYTUUAUUQUUIUUPYTUUACUUHU
      AUGUHUUNYTUUAUUQUUNUUPYTUUAUUMUUPABUUFUUHUUMUUPUFUUJUUFIZUUKUUHIZJZUUMUUP
      UULSIUUJUUKAUIUJCUULSUKULUMUNUGUHUOUPUUQUUEUUOUUQUUECHNZUQZPZHUUCURZUSOZQ
      ZUUOUUQUUPUUCSIZUUEUVFUTUUPYTUUAVAYTUUAUVGUUPDEFGVBZVCHCUUCSSVDVEUUQUVCUU
      OHUVEUUQUVAUVEIZJZUVCUVAVFVGZUUOUVJUUPUVCUVKUFUUPYTUUAUVIVHUVCUUPUVKUVCUU
      PUVBSIUVKCUVBSUKUVAVIVJVKVLUVJUVKUVCUUOUUQUVIUVKUVCUUOUFUUQUVIUVKJZJZUUOU
      VCUVBUUFIZUVBUUHIZUVBUULPZBUUHQAUUFQZRZUVADOZVFVGZUVAEOZVFVGZUVMUVRUFZUVT
      VMUVSVFPZUWCUVSVFVNUWDUUQUVLUVRUWDUUQUVLTZUVOUVRUWEUUAUVAEVOUVKUVAUSIZUVO
      UWDUUPYTUUAUVLVPUWEUVAUUCDVQZEUWEUWDUVAUWGVOZUWDUUQUVLVAUWEUVAUUCVOZUWDUW
      HUTUWEUVIUWIUWDUUQUVIUVKVRZUVIUVAUVDIUWIUVEUVDUVAUVDUSVSVTUVAUUCWAVLZVLUV
      ADUUCWBVLWCUWGEDVQZEUWGEDKZDVQUWLUUCUWMDDEWDWEEDWFWGEDWHWIWJUWDUUQUVIUVKW
      KUWEUVIUWFUWJUVEUSUVAUVDUSWLVTZVLUVAEGWMWNUVOUVNUVQWOVLUHWPUWBVMUWAVFPZUW
      CUWAVFVNUWOUUQUVLUVRUWOUUQUVLTZUVNUVRUWPYTUVADVOUVKUWFUVNUWOUUPYTUUAUVLWQ
      UWPUVAUUCEVQZDUWPUWOUVAUWQVOZUWOUUQUVLVAUWPUWIUWOUWRUTUWPUVIUWIUWOUUQUVIU
      VKVRZUWKVLUVAEUUCWBVLWCUWQDEVQDDEWFDEWHWIWJUWOUUQUVIUVKWKUWPUVIUWFUWSUWNV
      LUVADFWMWNUVNUVOUVQWRVLUHWPUVTUWBJZUUQUVLUVRUWTUUQUVLTZUVQUVRUXAUVSUQZUUF
      IZUWAUQZUUHIZUVBUXBUXDOZPZUVQUXAYTUVSDVOZUVTUVSUSIZUXCUWTUUPYTUUAUVLWQUXH
      UXAUVADWLUMUVTUWBUUQUVLWSUXAUVIUWFUXIUWTUUQUVIUVKVRZUWNUWFUVSUVAVOUXIUVAD
      VSUVAUVSWTXAXBUVSDFWMWNUXAUUAUWAEVOZUWBUWAUSIZUXEUWTUUPYTUUAUVLVPUXKUXAUV
      AEWLUMUVTUWBUUQUVLXCUXAUVIUWFUXLUXJUWNUWFUWAUVAVOUXLUVAEVSUVAUWAWTXAXBUWA
      EGWMWNUXAUVIUWIUXGUXJUWKUWIUVBUVSUWAKZUQUXFUWIUVAUXMUWIUXMUVAUUCOZUVAUVAD
      EXDUWIUXNUVAPUVAUUCXEXFXGXHUVSUWAXIXJXBUVPUXGUVBUXBUUKOZPABUXBUXDUUFUUHUU
      JUXBPUULUXOUVBUUJUXBUUKXKXLUUKUXDPUXOUXFUVBUUKUXDUXBXMXLXNXOUVQUVNUVOXPVL
      UHXQUVCUUGUVNUUIUVOUUNUVQCUVBUUFUKCUVBUUHUKUVCUUMUVPABUUFUUHCUVBUULXRXSXT
      YAYBYCYDYEYFUUQUUGUUEUUIUUNUUQUUFUUDCYTUUAUUFUUDVOZUUPUUBUVGDUUCVOUXPUVHD
      EYGDUUCSYHYIVCZYJUUQUUHUUDCYTUUAUUHUUDVOZUUPUUBUVGEUUCVOUXRUVHEDYKEUUCSYH
      YIVCZYJUUQUUMUUEABUUFUUHUUQUUTUUJUUDIZUUKUUDIZJZUUMUUEUFZUUQUURUXTUUSUYAU
      UQUUFUUDUUJUXQYJUUQUUHUUDUUKUXSYJYLUYBUULUUDIUYCUUJUUKUUCYMUULUUDCYNVLYOY
      PYQYRYS $.
      $( [26-Nov-2013] $) $( [15-Nov-2009] $)
  $}

  ${
    $d m n u v x y J $.  $d x y X $.
    hausnei2.1 $e |- X = U. J $.
    $( The Hausdorff condition still holds if one considers general
       neighborhoods instead of open sets.  (Contributed by Jeff Hankins,
       5-Sep-2009.) $)
    hausnei2 $p |- ( J e. Top -> ( J e. Haus <-> A. x e. X A. y e. X ( x =/= y
    -> E. u e. ( ( nei ` J ) ` { x } ) E. v e. ( ( nei ` J ) ` { y } ) ( u i^i
    v ) = (/) ) ) ) $=
      ( vm vn ctop wcel cha cv wne cin c0 wceq w3a wrex wi wral csn cnei cfv
      ishaus baib wa simp1 adantr simp2 adantl opnneip syl111anc simp3 ineq1
      eqeq1d ineq2 rcla42ev ex 3expib r19.23advv wss neii2 visset snss anbi1i
      simp1l simp2l ss2in ssn0 necon4d syl ad2ant2l 3impia 3jca 3exp syl5bir
      com3r imp 3adant1 reximdv com34 3imp com24 imp3a syl2and impbid imbi2d
      2ralbidv bitrd ) EJKZELKZAMZBMZNZWMHMZKZWNIMZKZWPWROZPQZRZIESZHESZTZBFUAA
      FUAZWODMZCMZOZPQZCWNUBZEUCUDZUDZSDWMUBZXLUDZSZTZBFUAAFUAWLWKXFABIHEFGUEUF
      WKXEXQABFFWKXDXPWOWKXDXPWKXBXPHIEEWKWPEKZWREKZXBXPTWKXRXSRZXBXPXTXBUGZWPX
      OKZWRXMKZXAXPYAWKXRWQYBXTWKXBWKXRXSUHUIZXTXRXBWKXRXSUJUIXBWQXTWQWSXAUHUKW
      MEWPULUMYAWKXSWSYCYDXTXSXBWKXRXSUNUIXBWSXTWQWSXAUJUKWNEWRULUMXBXAXTWQWSXA
      UNUKXJXAWPXHOZPQDCWPWRXOXMXGWPQXIYEPXGWPXHUOUPXHWRQYEWTPXHWRWPUQUPURUMUSU
      TVAWKXJXDDCXOXMWKXGXOKZXNWPVBZWPXGVBZUGZHESZXHXMKZXKWRVBZWRXHVBZUGZIESZXJ
      XDTZWKYFYJXNHEXGVCUSWKYKYOXKIEXHVCUSWKYJYOYPWKXJYOYJXDWKXJYOYJXDTWKXJYORZ
      YIXCHEYIWQYHUGZYQXCWQYGYHWMWPAVDVEVFWKXJYOYRXCTWKXJYRYOXCWKXJYRYOXCTWKXJY
      RRYNXBIEXJYRYNXBTZWKXJYRYSYRYNXJXBYNWSYMUGZYRXJXBTWSYLYMWNWRBVDVEVFYRYTXJ
      XBYRYTXJRWQWSXAWQYHYTXJVGYRWSYMXJVHYRYTXJXAYHYMXJXATZWQWSYHYMUGWTXIVBZUUA
      WPXGWRXHVIUUBWTPXIPUUBWTPNXIPNWTXIVJUSVKVLVMVNVOVPVQVRVSVTWAVPWBWCVQWAVPW
      DWEWFVAWGWHWIWJ $.
      $( [1-Dec-2010] $) $( [5-Sep-2009] $)
  $}

  ${
    $d R u v w x z $.  $d S u v w x z $.  $d T u v w x z $.  $d X u v w x z $.
    $d Y u v w x z $.  $d Z u v w x z $.
    tx1cn.1 $e |- T = ( R tX S ) $.
    tx1cn.2 $e |- X = U. R $.
    tx1cn.3 $e |- Y = U. S $.
    tx1cn.4 $e |- Z = ( X X. Y ) $.
    $( Continuity of the first projection map of a topological product.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    tx1cn $p |- ( ( R e. Top /\ S e. Top ) -> ( 1st |` Z ) e. ( T Cn R ) ) $=
      ( vw vz vu vv ctop wcel wa c1st cres ccn co cuni wf ccnv cv cima wral wb
      txtop simpl eqid iscn syl11anc cxp f1stres wceq a1i reseq2d feq1d txuni
      feq2d bitrd mpbiri wrex cab wss ctg cfv ctb txbas bastg syl ctx txval
      syl5eq sseqtr4d adantr simpr topopn ad2antlr reseq2i cnveqi imaeq1i crn
      imassrn sseli cdm cin cvv dfdm4 dmres eqtr3i wfo fo1st fof fdm ax-mp
      ineq2i inv1 3eqtri syl6eleq wi wfun ffun fvimacnvi mpan fvres eleq1d
      biimpd c2nd xpss xp2nd elxp7 biimpri syl12anc ex syldd com23 syl5 mpdi
      ssrdv eltopss xpss1 sseld imp adantllr xp1st adantl eqeltrd r19.21aiva
      adantlr fdmi syl6sseqr funimass4 sylancr mpbird funimass3 mpbid eqssd
      xpeq1 eqeq2d xpeq2 rcla42ev syl111anc uniexg syl5eqel xpexg syl2an fex
      cnvexg imaexg 3syl eqeq1 2rexbidv elabg sseldd mpbir2and ) AOPZBOPZQZRFSZ
      CATUAPZCUBZDUVAUCZUVAUDZKUEZUFZCPZKAUGZUUTCOPUURUVBUVDUVIQUHABCGUIUURUUSU
      JKUVACAUVCDUVCUKHULUMUUTUVDDEUNZDRUVJSZUCZDEUOZUUTUVDUVCDUVKUCUVLUUTUVCDU
      VAUVKUUTFUVJRFUVJUPUUTJUQURUSUUTUVCUVJDUVKABCDEGHIUTVAVBVCUUTUVHKAUUTUVFA
      PZQZLUEZMUEZNUEZUNZUPZNBVDMAVDZLVEZCUVGUUTUWBCVFUVNUUTUWBUWBVGVHZCUUTUWBV
      IPUWBUWCVFLNMABVJUWBVKVLUUTCABVMUAUWCGLNMABVNVOVPVQUVOUVGUWBPZUVGUVSUPZNB
      VDMAVDZUVOUVNEBPZUVGUVFEUNZUPZUWFUUTUVNVRUUSUWGUURUVNBEIVSVTUVOUVGUVKUDZU
      VFUFZUWHUVEUWJUVFUVAUVKFUVJRJWAZWBWCUVOUWKUWHUVOLUWKUWHUVOUVPUWKPZUVPUVJP
      ZUVPUWHPZUWMUVPUWJWDZUVJUWKUWPUVPUWJUVFWEWFUWPUVJRWGZWHZUVJWIWHUVJUVKWGZU
      WPUWRUVKWJRUVJWKWLUWQWIUVJWIWIRWMZUWQWIUPZWNUWTWIWIRUCUXAWIWIRWOWIWIRWPVL
      WQWRUVJWSWTXAUWMUVPUVKVHZUVFPZUVOUWNUWOXBUVKXCZUWMUXCUVLUXDUVMUVJDUVKXDWQ
      ZUVPUVFUVKXEXFUVOUWNUXCUWOUVOUWNUXCUVPRVHZUVFPZUWOUWNUXCUXGXBXBUVOUWNUXCU
      XGUWNUXBUXFUVFUVPUVJRXGZXHXIUQUWNUXGUWOXBXBUVOUWNUXGUWOUWNUXGQUVPWIWIUNZP
      ZUXGUVPXJVHEPZUWOUWNUXJUXGUVJUXIUVPDEXKWFVQUWNUXGVRUWNUXKUXGUVPDEXLVQUWOU
      XJUXGUXKQQUVPUVFEXMXNXOXPUQXQXRXSXTYAUVOUVKUWHUFUVFVFZUWHUWKVFZUVOUXLUXCL
      UWHUGZUVOUXCLUWHUVOUWOQZUXBUXFUVFUXOUWNUXBUXFUPUURUVNUWOUWNUUSUURUVNQZUWO
      UWNUXPUWHUVJUVPUXPUVFDVFUWHUVJVFZUVFADHYBUVFDEYCVLZYDYEYFUXHVLUWOUXGUVOUV
      PUVFEYGYHYIYJUVOUXDUWHUWSVFZUXLUXNUHUXEUVOUWHUVJUWSUURUVNUXQUUSUXRYKUVJDU
      VKUVMYLYMZLUWHUVFUVKYNYOYPUVOUXDUXSUXLUXMUHUXEUXTUWHUVFUVKYQYOYRYSVOUWEUW
      IUVGUVFUVRUNZUPMNUVFEABUVQUVFUPUVSUYAUVGUVQUVFUVRYTUUAUVREUPUYAUWHUVGUVRE
      UVFUUBUUAUUCUUDUVOUVGWIPZUWDUWFUHUVOUVAWIPUVEWIPUYBUVOUVAUVKWIUWLUUTUVKWI
      PZUVNUUTUVLUVJWIPZUYCUVMUURDWIPEWIPUYDUUSUURDAUBWIHAOUUEUUFUUSEBUBWIIBOUU
      EUUFDEWIWIUUGUUHUVJDWIUVKUUIYOVQUUFUVAWIUUJUVEUVFWIUUKUULUWAUWFLUVGWIUVPU
      VGUPUVTUWEMNABUVPUVGUVSUUMUUNUUOVLYPUUPYJUUQ $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d R u v w x z $.  $d S u v w x z $.  $d T u v w x z $.  $d X u v w x z $.
    $d Y u v w x z $.  $d Z u v w x z $.
    tx2cn.1 $e |- T = ( R tX S ) $.
    tx2cn.2 $e |- X = U. R $.
    tx2cn.3 $e |- Y = U. S $.
    tx2cn.4 $e |- Z = ( X X. Y ) $.
    $( Continuity of the second projection map of a topological product.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    tx2cn $p |- ( ( R e. Top /\ S e. Top ) -> ( 2nd |` Z ) e. ( T Cn S ) ) $=
      ( vw vz vu vv ctop wcel wa c2nd cres ccn co cuni wf ccnv cv cima wral wb
      txtop eqid iscn sylancom cxp f2ndres wceq a1i reseq2d feq1d txuni feq2d
      bitrd mpbiri wrex cab wss ctg cfv ctb txbas bastg syl ctx txval syl5eq
      sseqtr4d adantr topopn ad2antrr simpr reseq2i cnveqi imaeq1i crn imassrn
      sseli cdm cin cvv dfdm4 dmres eqtr3i wfo fo2nd fof fdm ax-mp ineq2i inv1
      3eqtri syl6eleq wi wfun ffun fvimacnvi mpan fvres eleq1d biimpd c1st
      xpss xp1st elxp7 biimpri syl12anc ex syldd com23 syl5 mpdi ssrdv eltopss
      xpss2 sseld imp adantlll xp2nd adantl eqeltrd r19.21aiva adantll fdmi
      syl6sseqr funimass4 sylancr mpbird funimass3 mpbid eqssd xpeq1 eqeq2d
      xpeq2 rcla42ev syl111anc uniexg syl5eqel xpexg syl2an fex cnvexg imaexg
      3syl eqeq1 2rexbidv elabg sseldd mpbir2and ) AOPZBOPZQZRFSZCBTUAPZCUBZEUU
      TUCZUUTUDZKUEZUFZCPZKBUGZUUQUURCOPUVAUVCUVHQUHABCGUIKUUTCBUVBEUVBUJIUKULU
      USUVCDEUMZERUVISZUCZDEUNZUUSUVCUVBEUVJUCUVKUUSUVBEUUTUVJUUSFUVIRFUVIUOUUS
      JUPUQURUUSUVBUVIEUVJABCDEGHIUSUTVAVBUUSUVGKBUUSUVEBPZQZLUEZMUEZNUEZUMZUOZ
      NBVCMAVCZLVDZCUVFUUSUWACVEUVMUUSUWAUWAVFVGZCUUSUWAVHPUWAUWBVELNMABVIUWAVJ
      VKUUSCABVLUAUWBGLNMABVMVNVOVPUVNUVFUWAPZUVFUVRUOZNBVCMAVCZUVNDAPZUVMUVFDU
      VEUMZUOZUWEUUQUWFUURUVMADHVQVRUUSUVMVSUVNUVFUVJUDZUVEUFZUWGUVDUWIUVEUUTUV
      JFUVIRJVTZWAWBUVNUWJUWGUVNLUWJUWGUVNUVOUWJPZUVOUVIPZUVOUWGPZUWLUVOUWIWCZU
      VIUWJUWOUVOUWIUVEWDWEUWOUVIRWFZWGZUVIWHWGUVIUVJWFZUWOUWQUVJWIRUVIWJWKUWPW
      HUVIWHWHRWLZUWPWHUOZWMUWSWHWHRUCUWTWHWHRWNWHWHRWOVKWPWQUVIWRWSWTUWLUVOUVJ
      VGZUVEPZUVNUWMUWNXAUVJXBZUWLUXBUVKUXCUVLUVIEUVJXCWPZUVOUVEUVJXDXEUVNUWMUX
      BUWNUVNUWMUXBUVORVGZUVEPZUWNUWMUXBUXFXAXAUVNUWMUXBUXFUWMUXAUXEUVEUVOUVIRX
      FZXGXHUPUWMUXFUWNXAXAUVNUWMUXFUWNUWMUXFQUVOWHWHUMZPZUVOXIVGDPZUXFUWNUWMUX
      IUXFUVIUXHUVODEXJWEVPUWMUXJUXFUVODEXKVPUWMUXFVSUWNUXIUXJUXFQQUVODUVEXLXMX
      NXOUPXPXQXRXSXTUVNUVJUWGUFUVEVEZUWGUWJVEZUVNUXKUXBLUWGUGZUVNUXBLUWGUVNUWN
      QZUXAUXEUVEUXNUWMUXAUXEUOUURUVMUWNUWMUUQUURUVMQZUWNUWMUXOUWGUVIUVOUXOUVEE
      VEUWGUVIVEZUVEBEIYAUVEEDYBVKZYCYDYEUXGVKUWNUXFUVNUVODUVEYFYGYHYIUVNUXCUWG
      UWRVEZUXKUXMUHUXDUVNUWGUVIUWRUURUVMUXPUUQUXQYJUVIEUVJUVLYKYLZLUWGUVEUVJYM
      YNYOUVNUXCUXRUXKUXLUHUXDUXSUWGUVEUVJYPYNYQYRVNUWDUWHUVFDUVQUMZUOMNDUVEABU
      VPDUOUVRUXTUVFUVPDUVQYSYTUVQUVEUOUXTUWGUVFUVQUVEDUUAYTUUBUUCUVNUVFWHPZUWC
      UWEUHUVNUUTWHPUVDWHPUYAUVNUUTUVJWHUWKUUSUVJWHPZUVMUUSUVKUVIWHPZUYBUVLUUQD
      WHPEWHPUYCUURUUQDAUBWHHAOUUDUUEUUREBUBWHIBOUUDUUEDEWHWHUUFUUGUVIEWHUVJUUH
      YNVPUUEUUTWHUUIUVDUVEWHUUJUUKUVTUWELUVFWHUVOUVFUOUVSUWDMNABUVOUVFUVRUULUU
      MUUNVKYOUUOYIUUP $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d A h x y z $.  $d B h x y z $.  $d C h x y z $.  $d D h x y z $.
    $d F h x y z $.  $d G h x y z $.  $d P h x y z $.  $d Q h x y z $.
    upxp.1 $e |- P = ( 1st |` ( B X. C ) ) $.
    upxp.2 $e |- Q = ( 2nd |` ( B X. C ) ) $.
    $( Universal property of the Cartesian product considered as a categorical
       product in the category of sets.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    upxp $p |- ( ( A e. D /\ F : A --> B /\ G : A --> C ) ->
        E! h ( h : A --> ( B X. C ) /\ F = ( P o. h ) /\ G = ( Q o. h ) ) ) $=
      ( vx vy vz wcel wf w3a cxp cv ccom wceq weu cfv cop wa copab cvv
      opabex2g eueq sylib 3ad2ant1 wral wfn wb ffn adantl ffvelrn opelxpi
      syl2an anandirs r19.21aiva 3adant1 eqid fopab2 syl adantr eqfnfv
      syl11anc eqidd c1st c2nd xpss sseli 3ad2antl1 adantll cres fveq1 coeq1i
      fveq1i syl6eq 3ad2ant2 ad2antlr wfun cdm wss wfo fo1st fofn ax-mp ssv
      fnssres mp2an fnfun a1i ffun fdm eleq2d biimpar fvco syl111anc fvres
      3eqtrrd 3ad2ant3 fo2nd eqopi syl12anc fveq2 opeq12d opex fvopab4 eqtr4d
      mpbir2and ex crn frn fnco fveq2d 3adantl1 fvex op1st 3eqtrd eqtr2d
      syl6eqr op2nd 3jca feq1 coeq2 eqeq2d 3anbi123d syl5ibrcom impbid eubidv
      mpbird ) ADOZABHPZACIPZQZABCRZGSZPZHEYSTZUAZIFYSTZUAZQZGUBYSLSZAOZMSUUFHU
      CZUUFIUCZUDZUAUELMUFZUAZGUBZYNYOUUMYPYNUUKUGOUUMLMAUUJDUHGUUKUIUJUKYQUUEU
      ULGYQUUEUULYQUUEUULYQUUEUEZUULAAUAZNSZYSUCZUUPUUKUCZUAZNAULZUUNYSAUMZUUKA
      UMZUULUUOUUTUEUNUUEUVAYQYTUUBUVAUUDAYRYSUOUKUPYQUVBUUEYQAYRUUKPZUVBYQUUJY
      ROZLAULZUVCYOYPUVEYNYOYPUEUVDLAYOYPUUGUVDYOUUGUEUUHBOUUICOUVDYPUUGUEABUUF
      HUQACUUFIUQUUHUUIBCURUSUTVAVBLMAYRUUJUUKUUKVCZVDUJZAYRUUKUOVEZVFNAAYSUUKV
      GVHUUNAVIUUNUUSNAUUNUUPAOZUEZUUQUUPHUCZUUPIUCZUDZUURUVJUUQUGUGRZOZUUQVJUC
      ZUVKUAUUQVKUCZUVLUAUUQUVMUAUUEUVIUVOYQYTUUBUVIUVOUUDYTUVIUEUUQYROZUVOAYRU
      UPYSUQZYRUVNUUQBCVLVMVEVNVOUVJUVKUUPVJYRVPZYSTZUCZUUQUVTUCZUVPUUEUVKUWBUA
      ZYQUVIUUBYTUWDUUDUUBUVKUUPUUAUCUWBUUPHUUAVQUUPUUAUWAEUVTYSJVRVSVTWAWBUVJU
      VTWCZYSWCZUUPYSWDZOZUWBUWCUAUWEUVJUVTYRUMZUWEVJUGUMZYRUGWEZUWIUGUGVJWFUWJ
      WGUGUGVJWHWIYRWJZUGYRVJWKWLZYRUVTWMWIZWNUUEUWFYQUVIYTUUBUWFUUDAYRYSWOUKWB
      ZUUEUVIUWHYQYTUUBUVIUWHUUDYTUWHUVIYTUWGAUUPAYRYSWPWQWRVNVOZUUPUVTYSWSWTUV
      JUVRUWCUVPUAUUEUVIUVRYQYTUUBUVIUVRUUDUVSVNVOZUUQYRVJXAVEXBUVJUVLUUPVKYRVP
      ZYSTZUCZUUQUWRUCZUVQUUEUVLUWTUAZYQUVIUUDYTUXBUUBUUDUVLUUPUUCUCUWTUUPIUUCV
      QUUPUUCUWSFUWRYSKVRVSVTXCWBUVJUWRWCZUWFUWHUWTUXAUAUXCUVJUWRYRUMZUXCVKUGUM
      ZUWKUXDUGUGVKWFUXEXDUGUGVKWHWIUWLUGYRVKWKWLZYRUWRWMWIZWNUWOUWPUUPUWRYSWSW
      TUVJUVRUXAUVQUAUWQUUQYRVKXAVEXBUUQUVKUVLXEXFUVIUURUVMUAZUUNLMUUPUUJUVMAUU
      KUUFUUPUAUUHUVKUUIUVLUUFUUPHXGUUFUUPIXGXHUVFUVKUVLXIXJZUPXKVAXLXMYQUUEUUL
      UVCHEUUKTZUAZIFUUKTZUAZQYQUVCUXKUXMUVGYQHUVTUUKTZUXJYQHUXNUAZUUOUVKUUPUXN
      UCZUAZNAULZYQHAUMZUXNAUMZUXOUUOUXRUEUNYOYNUXSYPABHUOWAYQUWIUVBUUKXNYRWEZU
      XTUWIYQUWMWNUVHYQUVCUYAUVGAYRUUKXOVEZYRAUVTUUKXPWTNAAHUXNVGVHYQAVIZYQUXQN
      AYQUVIUEZUXPUURUVTUCZUVKUYDUWEUUKWCZUUPUUKWDZOZUXPUYEUAUWEUYDUWNWNUYDUVCU
      YFYQUVCUVIUVGVFAYRUUKWOVEZYQUYHUVIYQUYGAUUPYQUVCUYGAUAUVGAYRUUKWPVEWQWRZU
      UPUVTUUKWSWTUYDUYEUVMUVTUCZUVMVJUCZUVKUYDUURUVMUVTUVIUXHYQUXIUPZXQUYDUVMY
      ROZUYKUYLUAYOYPUVIUYNYNYOYPUVIUYNYOUVIUEUVKBOUVLCOUYNYPUVIUEABUUPHUQACUUP
      IUQUVKUVLBCURUSUTXRZUVMYRVJXAVEUYLUVKUAUYDUVKUVLUUPHXSZXTWNYAYBVAXLEUVTUU
      KJVRYCYQIUWRUUKTZUXLYQIUYQUAZUUOUVLUUPUYQUCZUAZNAULZYQIAUMZUYQAUMZUYRUUOV
      UAUEUNYPYNVUBYOACIUOXCYQUXDUVBUYAVUCUXDYQUXFWNUVHUYBYRAUWRUUKXPWTNAAIUYQV
      GVHUYCYQUYTNAUYDUYSUURUWRUCZUVLUYDUXCUYFUYHUYSVUDUAUXCUYDUXGWNUYIUYJUUPUW
      RUUKWSWTUYDVUDUVMUWRUCZUVMVKUCZUVLUYDUURUVMUWRUYMXQUYDUYNVUEVUFUAUYOUVMYR
      VKXAVEVUFUVLUAUYDUVKUVLUYPUUPIXSYDWNYAYBVAXLFUWRUUKKVRYCYEUULYTUVCUUBUXKU
      UDUXMAYRYSUUKYFUULUUAUXJHYSUUKEYGYHUULUUCUXLIYSUUKFYGYHYIYJYKYLYM $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d R h s t u v w x y z $.  $d S h s t u v w x y z $.
    $d X h s t u v w x y z $.  $d T h s t u v w x y z $.
    $d U h s t u v w x y z $.  $d Y h s t u v w x y z $.
    $d P h s t u v w x y z $.  $d Q h s t u v w x y z $.
    $d Z h s t u v w x y z $.  $d F h s t u v w x y z $.
    $d G h s t u v w x y z $.
    uptx.1 $e |- T = ( R tX S ) $.
    uptx.2 $e |- X = U. R $.
    uptx.3 $e |- Y = U. S $.
    uptx.4 $e |- Z = ( X X. Y ) $.
    uptx.5 $e |- P = ( 1st |` Z ) $.
    uptx.6 $e |- Q = ( 2nd |` Z ) $.
    $( Universal property of the binary topological product.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    uptx $p |- ( ( ( R e. Top /\ S e. Top /\ U e. Top ) /\
                          ( F e. ( U Cn R ) /\ G e. ( U Cn S ) ) ) ->
                  E! h e. ( U Cn T ) ( F = ( P o. h ) /\ G = ( Q o. h ) ) ) $=
      ( vx vy vz vs vw vu vv vt ctop wcel w3a ccn co wa cv ccom wceq wex wmo
      wreu cuni cfv cop copab cvv uniexg opabex2g syl 3ad2ant3 adantr wf ccnv
      cima wral wb txtop eqid iscn sylan2 ancoms 3impa cxp wi cnf 3expia
      3adant2 3adant1 anim12d imp ffvelrn opelxpi syl2an anandirs r19.21aiva
      fopab2 sylib txuni 3adant3 feq3 mpbird wrex cab wss ctg ctx txval syl5eq
      eleq2d ctb txbas eltg3 bitrd ciun imaiun simpll3 ssel2 visset eqeq1
      2rexbidv elab cin fveq2 opeq12d opex fvopab4 eleq1d fvex opelxp syl6bb
      adantl pm5.32da anandi wfn ffn elpreima 3syl elin bi2anan9 syl5bb
      3bitr4d eqrdv sylan cnima ex 3expa ancom1s 3adantl2 adantlrr adantrr
      3adantl1 adantlrl adantrl inopn syl111anc eqeltrd imaeq2 biimprcd syl6
      r19.23advv anassrs iunopn syl11anc syl5eqel syl5ibrcom expimpd 19.23adv
      sylbid r19.21aiv mpbir2and c1st cres crn wfo fo1st fofn ax-mp ssv
      fnssres mp2an a1i frn fnco eqfnfv eqidd wfun cdm fnfun ffun fdm biimpar
      fvco fveq2d fvres op1st 3eqtrd eqtr2d reseq2i eqtri coeq1i syl6eqr c2nd
      fo2nd op2nd jca32 eleq1 coeq2 eqeq2d anbi12d cla4egv sylc imdistani wal
      sylibd anim1d 3anass syl6ibr 19.21aiv weu upxp eumo 3expb 3ad2antl3 immo
      df-reu eu5 bitri sylanbrc ) CUGUHZDUGUHZFUGUHZUIZHFCUJUKUHZIFDUJUKUHZULZU
      LZGUMZFEUJUKZUHZHAUYNUNZUOZIBUYNUNZUOZULZULZGUPZVUBGUQZVUAGUYOURZUYMSUMZF
      USZUHZTUMVUFHUTZVUFIUTZVAZUOULSTVBZVCUHZVULUYOUHZHAVULUNZUOZIBVULUNZUOZUL
      ZULZVUCUYIVUMUYLUYHUYFVUMUYGUYHVUGVCUHZVUMFUGVDZSTVUGVUKVCVEVFVGVHUYMVUNV
      UPVURUYMVUNVUGEUSZVULVIZVULVJZUAUMZVKZFUHZUAEVLZUYIVUNVVDVVIULVMZUYLUYFUY
      GUYHVVJUYHUYFUYGULZVVJVVKUYHEUGUHZVVJCDEMVNZUAVULFEVUGVVCVUGVOZVVCVOZVPVQ
      VRVSVHUYMVVDVUGJKVTZVULVIZUYMVUGJHVIZVUGKIVIZULZVVQUYIUYLVVTUYIUYJVVRUYKV
      VSUYFUYHUYJVVRWAZUYGUYHUYFVWAUYHUYFUYJVVRHFCVUGJVVNNWBWCVRWDUYGUYHUYKVVSW
      AZUYFUYHUYGVWBUYHUYGUYKVVSIFDVUGKVVNOWBWCVRWEWFZWGZVVTVUKVVPUHZSVUGVLVVQV
      VTVWESVUGVVRVVSVUHVWEVVRVUHULVUIJUHVUJKUHVWEVVSVUHULVUGJVUFHWHVUGKVUFIWHV
      UIVUJJKWIWJWKWLSTVUGVVPVUKVULVULVOZWMWNZVFUYMVVCVVPUOZVVDVVQVMUYIVWHUYLUY
      FUYGVWHUYHCDEJKMNOWOWPZVHVVCVVPVUGVULWQVFWRUYMVVHUAEUYMVVFEUHZUBUMZUCUMZU
      DUMZUEUMZVTZUOZUEDWSUDCWSZUCWTZXAZVVFVWKUSZUOZULZUBUPZVVHUYIVWJVXCVMZUYLU
      YFUYGVXDUYHVVKVWJVVFVWRXBUTZUHZVXCVVKEVXEVVFVVKECDXCUKVXEMUCUEUDCDXDXEXFV
      VKVWRXGUHVXFVXCVMUCUEUDCDXHUBVVFVWRXIVFXJWPVHUYMVXBVVHUBUYMVWSVXAVVHUYMVW
      SULZVVHVXAVVEVWTVKZFUHVXGVXHUFVWKVVEUFUMZVKZXKZFUFVVEVWKXLVXGUYHVXJFUHZUF
      VWKVLVXKFUHUYFUYGUYHUYLVWSXMVXGVXLUFVWKUYMVWSVXIVWKUHZVXLVWSVXMULZUYMVXIV
      WOUOZUEDWSUDCWSZVXLVXNVXIVWRUHVXPVWKVWRVXIXNVWQVXPUCVXIUFXOVWLVXIUOVWPVXO
      UDUECDVWLVXIVWOXPXQXRWNUYMVXPVXLUYMVXOVXLUDUECDUYMVWMCUHZVWNDUHZULZVVEVWO
      VKZFUHZVXOVXLWAUYMVXSVYAUYMVXSULZVXTHVJVWMVKZIVJVWNVKZXSZFUYMVVTVXSVXTVYE
      UOVWDVVTVXSULZUAVXTVYEVYFVVFVUGUHZVVFVULUTZVWOUHZULZVYGVVFHUTZVWMUHZULZVY
      GVVFIUTZVWNUHZULZULZVVFVXTUHZVVFVYEUHZVYFVYJVYGVYLVYOULZULVYQVYFVYGVYIVYT
      VYGVYIVYTVMVYFVYGVYIVYKVYNVAZVWOUHVYTVYGVYHWUAVWOSTVVFVUKWUAVUGVULVUFVVFU
      OVUIVYKVUJVYNVUFVVFHXTVUFVVFIXTYAVWFVYKVYNYBYCZYDVYKVYNVWMVWNVVFIYEZYFYGY
      HYIVYGVYLVYOYJYGVVTVYRVYJVMZVXSVVTVVQVULVUGYKZWUDVWGVUGVVPVULYLZVUGVVFVWO
      VULYMYNVHVVTVYSVYQVMVXSVYSVVFVYCUHZVVFVYDUHZULVVTVYQVVFVYCVYDYOVVRWUGVYMV
      VSWUHVYPVVRHVUGYKZWUGVYMVMVUGJHYLZVUGVVFVWMHYMVFVVSIVUGYKZWUHVYPVMVUGKIYL
      ZVUGVVFVWNIYMVFYPYQVHYRYSYTVYBUYHVYCFUHZVYDFUHZVYEFUHUYFUYGUYHUYLVXSXMUYM
      VXQWUMVXRUYIUYJVXQWUMUYKUYIUYJULVXQWUMUYFUYHUYJVXQWUMWAZUYGUYHUYFUYJWUOUY
      HUYFUYJWUOUYHUYFUYJUIVXQWUMVWMHFCUUAUUBUUCUUDUUEWGUUFUUGUYMVXRWUNVXQUYIUY
      KVXRWUNUYJUYIUYKULVXRWUNUYGUYHUYKVXRWUNWAZUYFUYHUYGUYKWUPUYHUYGUYKWUPUYHU
      YGUYKUIVXRWUNVWNIFDUUAUUBUUCUUDUUHWGUUIUUJVYCVYDFUUKUULUUMUUBVXOVXLVYAVXO
      VXJVXTFVXIVWOVVEUUNYDUUOUUPUUQWGVQUURWLUFVWKVXJFUUSUUTUVAVXAVVGVXHFVVFVWT
      VVEUUNYDUVBUVCUVDUVEUVFUVGUYMVVTVUPVWDVVTHUVHVVPUVIZVULUNZVUOVVTHWURUOZVU
      GVUGUOZVYKVVFWURUTZUOZUAVUGVLZVVTWUIWURVUGYKZWUSWUTWVCULVMVVRWUIVVSWUJVHV
      VTWUQVVPYKZWUEVULUVJVVPXAZWVDWVEVVTUVHVCYKZVVPVCXAZWVEVCVCUVHUVKWVGUVLVCV
      CUVHUVMUVNVVPUVOZVCVVPUVHUVPUVQZUVRVVTVVQWUEVWGWUFVFZVVTVVQWVFVWGVUGVVPVU
      LUVSVFZVVPVUGWUQVULUVTUULUAVUGVUGHWURUWAUUTVVTVUGUWBZVVTWVBUAVUGVVTVYGULZ
      WVAVYHWUQUTZVYKWVNWUQUWCZVULUWCZVVFVULUWDZUHZWVAWVOUOWVPWVNWVEWVPWVJVVPWU
      QUWEUVNUVRWVNVVQWVQVVTVVQVYGVWGVHVUGVVPVULUWFVFZVVTWVSVYGVVTWVRVUGVVFVVTV
      VQWVRVUGUOVWGVUGVVPVULUWGVFXFUWHZVVFWUQVULUWIUULWVNWVOWUAWUQUTZWUAUVHUTZV
      YKWVNVYHWUAWUQVYGVYHWUAUOVVTWUBYHZUWJWVNWUAVVPUHZWWBWWCUOVVRVVSVYGWWEVVRV
      YGULVYKJUHVYNKUHWWEVVSVYGULVUGJVVFHWHVUGKVVFIWHVYKVYNJKWIWJWKZWUAVVPUVHUW
      KVFWWCVYKUOWVNVYKVYNVVFHYEZUWLUVRUWMUWNWLUVGAWUQVULAUVHLUVIWUQQLVVPUVHPUW
      OUWPZUWQUWRVFUYMVVTVURVWDVVTIUWSVVPUVIZVULUNZVUQVVTIWWJUOZWUTVYNVVFWWJUTZ
      UOZUAVUGVLZVVTWUKWWJVUGYKZWWKWUTWWNULVMVVSWUKVVRWULYHVVTWWIVVPYKZWUEWVFWW
      OWWPVVTUWSVCYKZWVHWWPVCVCUWSUVKWWQUWTVCVCUWSUVMUVNWVIVCVVPUWSUVPUVQZUVRWV
      KWVLVVPVUGWWIVULUVTUULUAVUGVUGIWWJUWAUUTWVMVVTWWMUAVUGWVNWWLVYHWWIUTZVYNW
      VNWWIUWCZWVQWVSWWLWWSUOWWTWVNWWPWWTWWRVVPWWIUWEUVNUVRWVTWWAVVFWWIVULUWIUU
      LWVNWWSWUAWWIUTZWUAUWSUTZVYNWVNVYHWUAWWIWWDUWJWVNWWEWXAWXBUOWWFWUAVVPUWSU
      WKVFWXBVYNUOWVNVYKVYNWWGWUCUXAUVRUWMUWNWLUVGBWWIVULBUWSLUVIWWIRLVVPUWSPUW
      OUWPZUWQUWRVFUXBVUBVUTGVULVCUYNVULUOZUYPVUNVUAVUSUYNVULUYOUXCWXDUYRVUPUYT
      VURWXDUYQVUOHUYNVULAUXDUXEWXDUYSVUQIUYNVULBUXDUXEUXFUXFUXGUXHUYMUYIVVTULZ
      VUDUYIUYLVVTVWCUXIWXEVUBVUGVVPUYNVIZUYRUYTUIZWAZGUXJWXGGUQZVUDWXEWXHGWXEV
      UBWXFVUAULWXGWXEUYPWXFVUAUYIUYPWXFWAVVTUYIUYPVUGVVCUYNVIZWXFUYFUYGUYHUYPW
      XJWAZUYHVVKWXKVVKUYHVVLWXKVVMUYHVVLUYPWXJUYNFEVUGVVCVVNVVOWBWCVQVRVSUYIVW
      HWXJWXFVMVWIVVCVVPVUGUYNWQVFUXKVHUXLWXFUYRUYTUXMUXNUXOUYHUYFVVTWXIUYGUYHV
      VAVVTWXIVVBVVAVVRVVSWXIVVAVVRVVSUIWXGGUXPWXIVUGJKVCABGHIWWHWXCUXQWXGGUXRV
      FUXSYTUXTVUBWXGGUYAUXHVFVUEVUBGUXPVUCVUDULVUAGUYOUYBVUBGUYCUYDUYE $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d R h x y $.  $d S h x y $.  $d T h x y $.  $d U h x y $.  $d W h x y $.
    $d X h x y $.  $d Y h x y $.  $d Z h x y $.  $d P h x y $.  $d Q h x y $.
    $d F h x y $.
    txcn.1 $e |- T = ( R tX S ) $.
    txcn.2 $e |- X = U. R $.
    txcn.3 $e |- Y = U. S $.
    txcn.4 $e |- Z = ( X X. Y ) $.
    txcn.5 $e |- W = U. U $.
    txcn.6 $e |- P = ( 1st |` Z ) $.
    txcn.7 $e |- Q = ( 2nd |` Z ) $.
    $( A map into the product of two topological spaces is continuous iff both
       of its projections are continuous.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    txcn $p |- ( ( ( R e. Top /\ S e. Top /\ U e. Top ) /\ F : W --> Z ) ->
                  ( F e. ( U Cn T ) <-> ( ( P o. F ) e. ( U Cn R ) /\
                                          ( Q o. F ) e. ( U Cn S ) ) ) ) $=
      ( vh vx vy ctop wcel w3a wf wa ccn co ccom wi c1st cres tx1cn syl5eqel
      3adant3 simp3 txtop simp1 cnco ex syl111anc mpan2d c2nd tx2cn simp2 jcad
      adantr cv wceq wrex wreu uptx reurex syl cnf 3expia ancoms 3adant2
      3adant1 anim12d cxp cuni eqid sylan2 3impa wb txuni feq3 sylibd ad2antrr
      simpr simpll simplr 3jca adantll ax-mp biimpi eqidd ad2antlr wal weu cvv
      uniexg reseq2i eqtri upxp 3expb sylan 3ad2antl3 adantlr wex feq1 coeq2
      eqeq2d 3anbi123d eu4 simprbi visset fex ad3antrrr anbi1d equequ1 imbi12d
      anbi2d eqeq2 sylan9bb cla42gv sylancr mpd mp2and exp31 eleq1 biimpd syl8
      com24 mpdd r19.23adv syld impbid ) CUBUCZDUBUCZFUBUCZUDZHKGUEZUFZGFEUGUHZ
      UCZAGUIZFCUGUHUCZBGUIZFDUGUHUCZUFZUUCUUGUULUJUUDUUCUUGUUIUUKUUCUUGAECUGUH
      ZUCZUUIYTUUAUUNUUBYTUUAUFZAUKKULZUUMQCDEIJKLMNOUMUNUOUUCUUBEUBUCZYTUUGUUN
      UFZUUIUJYTUUAUUBUPZYTUUAUUQUUBCDELUQZUOZYTUUAUUBURUUBUUQYTUDUURUUIGAFECUS
      UTVAVBUUCUUGBEDUGUHZUCZUUKYTUUAUVCUUBUUOBVCKULZUVBRCDEIJKLMNOVDUNUOUUCUUB
      UUQUUAUUGUVCUFZUUKUJUUSUVAYTUUAUUBVEUUBUUQUUAUDUVEUUKGBFEDUSUTVAVBVFVGUUE
      UULUUHASVHZUIZVIZUUJBUVFUIZVIZUFZSUUFVJZUUGUUCUULUVLUJUUDUUCUULUVLUUCUULU
      FUVKSUUFVKUVLABCDEFSUUHUUJIJKLMNOQRVLUVKSUUFVMVNUTVGUUEUULHIUUHUEZHJUUJUE
      ZUFZUVLUUGUJZUUCUULUVOUJUUDUUCUUIUVMUUKUVNYTUUBUUIUVMUJZUUAUUBYTUVQUUBYTU
      UIUVMUUHFCHIPMVOVPVQVRUUAUUBUUKUVNUJZYTUUBUUAUVRUUBUUAUUKUVNUUJFDHJPNVOVP
      VQVSVTVGUUEUVOUVPUUEUVOUFZUVKUUGSUUFUVSUVFUUFUCZHIJWAZUVFUEZUVKUUGUJUUCUV
      TUWBUJUUDUVOUUCUVTHEWBZUVFUEZUWBYTUUAUUBUVTUWDUJZUUBUUOUWEUUOUUBUUQUWEUUT
      UUBUUQUVTUWDUVFFEHUWCPUWCWCVOVPWDVQWEUUCUWCUWAVIZUWDUWBWFYTUUAUWFUUBCDEIJ
      LMNWGUOUWCUWAHUVFWHVNWIWJUVSUVKUWBUVTUUGUVSUVKUWBUVFGVIZUVTUUGUJUVSUVKUWB
      UWGUVSUVKUFUWBUFZUWBUVHUVJUDZHUWAGUEZUUHUUHVIZUUJUUJVIZUDZUWGUVKUWBUWIUVS
      UVKUWBUFUWBUVHUVJUVKUWBWKUVHUVJUWBWLUVHUVJUWBWMWNWOUVSUWMUVKUWBUUDUWMUUCU
      VOUUDUWJUWKUWLUUDUWJKUWAVIUUDUWJWFOKUWAHGWHWPWQUUDUUHWRUUDUUJWRWNWSWJUWHH
      UWATVHZUEZUUHAUWNUIZVIZUUJBUWNUIZVIZUDZHUWAUAVHZUEZUUHAUXAUIZVIZUUJBUXAUI
      ZVIZUDZUFZUWNUXAVIZUJZUAWTTWTZUWIUWMUFZUWGUJZUWHUWTTXAZUXKUVSUXNUVKUWBUUC
      UVOUXNUUDUUBYTUVOUXNUUAUUBHXBUCZUVOUXNUUBHFWBXBPFUBXCUNZUXOUVMUVNUXNHIJXB
      ABTUUHUUJAUUPUKUWAULQKUWAUKOXDXEBUVDVCUWAULRKUWAVCOXDXEXFXGXHXIXJWJUXNUWT
      TXKUXKUWTUXGTUAUXIUWOUXBUWQUXDUWSUXFHUWAUWNUXAXLUXIUWPUXCUUHUWNUXAAXMXNUX
      IUWRUXEUUJUWNUXABXMXNXOXPXQVNUWHUVFXBUCGXBUCZUXKUXMUJSXRUUEUXQUVOUVKUWBUU
      BYTUUDUXQUUAUUDUUBUXQUUBUUDUXOUXQUXPHKXBGXSWDVQXIXTUXJUXMTUAUVFGXBXBUWNUV
      FVIZUXJUWIUXGUFZUVFUXAVIZUJUXAGVIZUXMUXRUXHUXSUXIUXTUXRUWTUWIUXGUXRUWOUWB
      UWQUVHUWSUVJHUWAUWNUVFXLUXRUWPUVGUUHUWNUVFAXMXNUXRUWRUVIUUJUWNUVFBXMXNXOY
      ATSUAYBYCUYAUXSUXLUXTUWGUYAUXGUWMUWIUYAUXBUWJUXDUWKUXFUWLHUWAUXAGXLUYAUXC
      UUHUUHUXAGAXMXNUYAUXEUUJUUJUXAGBXMXNXOYDUXAGUVFYEYCYFYGYHYIYJYKUWGUVTUUGU
      VFGUUFYLYMYNYOYPYQUTYRYPYS $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d T x y z $.  $d R x y z $.  $d S x y z $.  $d U x y z $.  $d W x y z $.
    $d F x y z $.  $d G x y z $.  $d H z $.
    txcnopab.1 $e |- T = ( R tX S ) $.
    txcnopab.2 $e |- W = U. U $.
    txcnopab.3 $e |- H = { <. x , y >. | ( x e. W /\
                                      y = <. ( F ` x ) , ( G ` x ) >. ) } $.
    $( A map into the product of two topological spaces is continuous if both
       of its projections are continuous.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    txcnopab $p |- ( ( ( R e. Top /\ S e. Top /\ U e. Top ) /\
                ( F e. ( U Cn R ) /\ G e. ( U Cn S ) ) )
                 -> H e. ( U Cn T ) ) $=
      ( vz ctop wcel w3a ccn co wa c1st cuni cxp cres ccom c2nd wf wi eqid cnf
      3expia ancoms 3adant2 3adant1 anim12d imp wceq cv cfv wral wfn wb crn
      wss cvv wfo fo1st fofn ax-mp ssv fnssres mp2an a1i cop ffvelrn opelxpi
      syl2an anandirs r19.21aiva fopab2 sylib ffn syl frn fnco syl111anc
      adantr eqfnfv syl11anc eqidd wfun cdm fnfun ffun fdm eleq2d biimpar fvco
      fveq2 opeq12d opex fvopab4 adantl fveq2d fvres fvex op1st syl6eq 3eqtrd
      mpbir2and eleq1d fo2nd op2nd anbi12d biimprcd mpd txcn syldan mpbird ) CO
      PZDOPZFOPZQZGFCRSZPZHFDRSZPZTZTZIFERSPZUACUBZDUBZUCZUDZIUEZYDPZUFYMUDZIUE
      ZYFPZTZYIJYKGUGZJYLHUGZTZYTYCYHUUCYCYEUUAYGUUBXTYBYEUUAUHZYAYBXTUUDYBXTYE
      UUAGFCJYKLYKUIZUJUKULUMYAYBYGUUBUHZXTYBYAUUFYBYAYGUUBHFDJYLLYLUIZUJUKULUN
      UOUPZYHUUCYTUHYCUUCYTYHUUCYPYEYSYGUUCYOGYDUUCYOGUQZJJUQZNURZYOUSZUUKGUSZU
      QZNJUTZUUCYOJVAZGJVAZUUIUUJUUOTVBUUCYNYMVAZIJVAZIVCYMVDZUUPUURUUCUAVEVAZY
      MVEVDZUURVEVEUAVFUVAVGVEVEUAVHVIYMVJZVEYMUAVKVLZVMUUCJYMIUGZUUSUUCAURZGUS
      ZUVFHUSZVNZYMPZAJUTUVEUUCUVJAJUUAUUBUVFJPZUVJUUAUVKTUVGYKPUVHYLPUVJUUBUVK
      TJYKUVFGVOJYLUVFHVOUVGUVHYKYLVPVQVRVSABJYMUVIIMVTWAZJYMIWBWCZUUCUVEUUTUVL
      JYMIWDWCZYMJYNIWEWFUUAUUQUUBJYKGWBWGNJJYOGWHWIUUCJWJZUUCUUNNJUUCUUKJPZTZU
      ULUUKIUSZYNUSZUUMUUKHUSZVNZYNUSZUUMUVQYNWKZIWKZUUKIWLZPZUULUVSUQUWCUVQUUR
      UWCUVDYMYNWMVIVMUVQUVEUWDUUCUVEUVPUVLWGJYMIWNWCZUUCUWFUVPUUCUWEJUUKUUCUVE
      UWEJUQUVLJYMIWOWCWPWQZUUKYNIWRWFUVQUVRUWAYNUVPUVRUWAUQUUCABUUKUVIUWAJIUVF
      UUKUQUVGUUMUVHUVTUVFUUKGWSUVFUUKHWSWTMUUMUVTXAXBXCZXDUVQUWBUWAUAUSZUUMUVQ
      UWAYMPZUWBUWJUQUUAUUBUVPUWKUUAUVPTUUMYKPUVTYLPUWKUUBUVPTJYKUUKGVOJYLUUKHV
      OUUMUVTYKYLVPVQVRZUWAYMUAXEWCUUMUVTUUKGXFZXGXHXIVSXJXKUUCYRHYFUUCYRHUQZUU
      JUUKYRUSZUVTUQZNJUTZUUCYRJVAZHJVAZUWNUUJUWQTVBUUCYQYMVAZUUSUUTUWRUWTUUCUF
      VEVAZUVBUWTVEVEUFVFUXAXLVEVEUFVHVIUVCVEYMUFVKVLZVMUVMUVNYMJYQIWEWFUUBUWSU
      UAJYLHWBXCNJJYRHWHWIUVOUUCUWPNJUVQUWOUVRYQUSZUWAYQUSZUVTUVQYQWKZUWDUWFUWO
      UXCUQUXEUVQUWTUXEUXBYMYQWMVIVMUWGUWHUUKYQIWRWFUVQUVRUWAYQUWIXDUVQUXDUWAUF
      USZUVTUVQUWKUXDUXFUQUWLUWAYMUFXEWCUUMUVTUWMUUKHXFXMXHXIVSXJXKXNXOXCXPYCYH
      UVEYJYTVBYIUUCUVEUUHUVLWCYNYQCDEFIJYKYLYMKUUEUUGYMUILYNUIYQUIXQXRXS $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d R w x y z $.  $d S w x y z $.  $d T w x y z $.  $d X w x y z $.
    $d Y w x y z $.  $d J w x y z $.  $d K w x y z $.  $d L w x y z $.
    $d F w x y z $.  $d G w x y z $.  $d H w $.
    2txcn.1 $e |- T = ( R tX S ) $.
    2txcn.2 $e |- L = ( J tX K ) $.
    2txcn.3 $e |- X = U. R $.
    2txcn.4 $e |- Y = U. S $.
    2txcn.5 $e |- H = { <. <. x , y >. , z >. | ( ( x e. X /\ y e. Y )
                                  /\ z = <. ( F ` x ) , ( G ` y ) >. ) } $.
    $( The product map between two topological product spaces is continuous.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    2txcn $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( J e. Top /\ K e. Top ) /\
            ( F e. ( R Cn J ) /\ G e. ( S Cn K ) ) ) -> H e. ( T Cn L ) ) $=
      ( vw ctop wcel wa ccn co w3a cv cuni c1st cxp cres ccom cfv c2nd cop
      wceq copab copab2 fveq2 opeq1d eqeq2d opeq2d oprabopab eqtri wf eqid cnf
      3adant3r 3adant2r 3adant1r 3adant3l 3adant2l 3adant1l jca wfun cdm ffun
      ad2antrr wfn cvv wss wfo fo1st fofn ax-mp ssv fnssres mp2an fnfun a1i
      fndm eleq2i biimpri adantl fvco syl111anc fvres fveq2d eqtr2d ad2antlr
      fo2nd opeq12d sylan pm5.32da txuni 3ad2ant1 eleq2d anbi1d bitr4d
      opabbidv syl5eq simp2l simp2r txtop simp1l tx1cn simp3l cnco syl32anc
      simp1r tx2cn simp3r txcnopab eqeltrd ) DUAUBZEUAUBZUCZJUAUBZKUAUBZUCZGDJU
      DUEUBZHEKUDUEUBZUCZUFZITUGZFUHZUBZCUGZYOGUIMNUJZUKZULZUMZYOHUNYSUKZULZUMZ
      UOZUPZUCZTCUQZFLUDUEZYNIYOYSUBZYRYOUIUMZGUMZYOUNUMZHUMZUOZUPZUCZTCUQZUUII
      AUGZMUBBUGZNUBUCYRUUTGUMZUVAHUMZUOZUPZUCABCURUUSSUVEYRUUMUVCUOZUPUUQABCTM
      NUUTUULUPZUVDUVFYRUVGUVBUUMUVCUUTUULGUSUTVAUVAUUNUPZUVFUUPYRUVHUVCUUOUUMU
      VAUUNHUSVBVAVCVDYNUURUUHTCYNUURUUKUUGUCUUHYNUUKUUQUUGYNUUKUCUUPUUFYRYNMJU
      HZGVEZNKUHZHVEZUCZUUKUUPUUFUPYNUVJUVLYEYJYMUVJYFYEYHYMUVJYIYEYHYKUVJYLGDJ
      MUVIQUVIVFVGVHVIVJYFYJYMUVLYEYFYIYMUVLYHYFYIYLUVLYKHEKNUVKRUVKVFVGVKVLVMV
      NUVMUUKUCZUUMUUBUUOUUEUVNUUBYOYTUMZGUMZUUMUVNGVOZYTVOZYOYTVPZUBZUUBUVPUPU
      VJUVQUVLUUKMUVIGVQVRUVRUVNYTYSVSZUVRUIVTVSZYSVTWAZUWAVTVTUIWBUWBWCVTVTUIW
      DWEYSWFZVTYSUIWGWHZYSYTWIWEWJUUKUVTUVMUVTUUKUVSYSYOUWAUVSYSUPUWEYSYTWKWEW
      LWMWNYOGYTWOWPUVNUVOUULGUUKUVOUULUPUVMYOYSUIWQWNWRWSUVNUUEYOUUCUMZHUMZUUO
      UVNHVOZUUCVOZYOUUCVPZUBZUUEUWGUPUVLUWHUVJUUKNUVKHVQWTUWIUVNUUCYSVSZUWIUNV
      TVSZUWCUWLVTVTUNWBUWMXAVTVTUNWDWEUWDVTYSUNWGWHZYSUUCWIWEWJUUKUWKUVMUWKUUK
      UWJYSYOUWLUWJYSUPUWNYSUUCWKWEWLWMWNYOHUUCWOWPUVNUWFUUNHUUKUWFUUNUPUVMYOYS
      UNWQWNWRWSXBXCVAXDYNYQUUKUUGYNYPYSYOYGYJYPYSUPYMDEFMNOQRXEXFXGXHXIXJXKYNY
      HYIFUAUBZUUAFJUDUEUBZUUDFKUDUEUBZUUIUUJUBYGYHYIYMXLZYGYHYIYMXMZYGYJUWOYMD
      EFOXNXFZYNUWOYEYHYTFDUDUEUBZYKUWPUWTYEYFYJYMXOUWRYGYJUXAYMDEFMNYSOQRYSVFZ
      XPXFYGYJYKYLXQYTGFDJXRXSYNUWOYFYIUUCFEUDUEUBZYLUWQUWTYEYFYJYMXTUWSYGYJUXC
      YMDEFMNYSOQRUXBYAXFYGYJYKYLYBUUCHFEKXRXSTCJKLFUUAUUDUUIYPPYPVFUUIVFYCXSYD
      $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

