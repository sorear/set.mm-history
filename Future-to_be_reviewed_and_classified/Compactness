$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Compactness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Comp $.

  $( Extend class notation with the class of all compact spaces. $)
  ccomp $a class Comp $.

  ${
    $d x y z $.
    $( Definition of a compact topology.  A topology is compact iff any open
       covering of its underlying set contains a finite sub-covering
       (Heine-Borel property).  Bourbaki TG I.59 prop C''' .  Note:  Bourbaki
       uses the term quasi-compact topology but it is not the modern usage
       (which we follow).  (Contributed by FL, 22-Dec-2008.) $)
    df-comp $a |- Comp = { x e. Top | A. y e. ~P x ( U. x = U. y
      -> E. z e. ( ~P y i^i Fin ) U. x = U. z ) } $.
  $}

  ${
    $d J x y z $.
    $( The predicate "is a compact topology".  (Contributed by FL,
       22-Dec-2008.) $)
    iscomp $p |- ( J e. Comp <-> ( J e. Top /\ A. y e. ~P J
      ( U. J = U. y -> E. z e. ( ~P y i^i Fin ) U. J = U. z ) ) ) $=
      ( vx cv cuni wceq cpw cfn cin wrex wi wral ctop ccomp pweq eqeq1d rexbidv
      unieq imbi12d raleqbidv df-comp elrab2 ) DEZFZAEZFZGZUEBEFZGZBUFHIJZKZLZA
      UDHZMCFZUGGZUOUIGZBUKKZLZACHZMDCNOUDCGZUMUSAUNUTUDCPVAUHUPULURVAUEUOUGUDC
      SZQVAUJUQBUKVAUEUOUIVBQRTUADABUBUC $.
      $( [1-Dec-2010] $) $( [22-Dec-2008] $)
  $}

  ${
    $d c d o r s u v x y F $.  $d c d o r s u v x y J $.
    $d c d o r s u v x y K $.  $d c d o r s u v x y X $.
    $d c d o r s u v x y Y $.
    cncomp.1 $e |- X = U. J $.
    cncomp.2 $e |- Y = U. K $.
    $( Compactness is respected by a continuous onto map.  (Contributed by Jeff
       Hankins, 12-Jul-2009.) $)
    cncomp $p |- ( ( ( J e. Comp /\ K e. Top ) /\ ( F : X -onto-> Y /\ F e. ( J
    Cn K ) ) ) -> K e. Comp ) $=
      ( vs vy vd vc wcel wa cuni cv wceq cfn wrex wi cima vu vv vr vx ccomp wfo
      vo ctop ccn cpw cin wral simplr iscomp wss visset elpw w3a ccnv cab eqeq1
      co rexbidv elab simp3 ssel2 cnima ex imp adantlrl sylan2 anassrs 3adantl2
      3expa 3adant3 eqeltrd 3exp rexlimdv syl5bi ssrdv sylibr unieq eqeq2d pweq
      abrexex ineq1d rexeqdv imbi12d rcla4v syl ciun cdm crn wfn fofn fndm forn
      cnvimarndm simpr imaeq2d simpl 3eqtr3rd imaeq2i 3eqtr3g syl11anc ad2antrl
      a1i 3ad2ant1 imaeq2 3ad2ant2 imaiun syl6eq cvv cnvexg imaexg ad2antll a1d
      eqtrd ralrimiv dfiun2g elin anbi1i bitri ssel eqtr elssuni syl5ibr sseq2i
      sseq2 foimacnv eqcomd sylan2br eleq1 biimprd syl6bir com13 com4t sylanbrc
      syl6 sylib com14 sylcom com12 com15 rexlimiv sylbi imp31 3adantl3 adantrr
      syl5 copab rnopab2 cdom wbr eqid dffn4 fodomfi ancoms sylan domfi adantll
      fnopab2g mpdan syl5eqelr adantld 3imp1 foima 3ad2ant3 rcla4ev syld syl7bi
      embantd com24 ralrimdv exp43 com4l imp5d exp31 com4r imp43 ) BUELZCUHLZMD
      EAUFZABCUIVBZLZMZMUWBCNZUAOZNZPZUWGUBOZNZPZUBUWHUJZQUKZRZSZUACUJZULZCUELU
      WAUWBUWFUMUWAUWBUWCUWEUWSUWBUWCUWEUWAUWSUWBUWCUWEUWAUWSSUWABUHLZBNZUCOZNZ
      PZUXAHOZNZPZHUXBUJZQUKZRZSZUCBUJZULZMUWBUWCMUWEMUWSUCHBUNUWBUWCUWEUWTUXMU
      WSUWTUWBUWCUWEUXMUWSSZUWTUWBUWCUWEUXNUWTUWBMZUWFMZUXMUWQUAUWRUWHUWRLUWHCU
      OZUXPUXMUWQUWHCUAUPZUQUXPUWJUXQUXMUWPUXPUWJUXQUXMUWPSUXPUWJUXQURZUXMUXAUD
      OZAUSZIOZTZPZIUWHRZUDUTZNZPZUXGHUYFUJZQUKZRZSZUWPUXSUYFUXLLZUXMUYLSUXSUYF
      BUOUYMUXSUGUYFBUGOZUYFLUYNUYCPZIUWHRZUXSUYNBLZUYEUYPUDUYNUGUPZUXTUYNPUYDU
      YOIUWHUXTUYNUYCVAVCVDUXSUYOUYQIUWHUXSUYBUWHLZUYOUYQUXSUYSUYOURUYNUYCBUXSU
      YSUYOVEUXSUYSUYCBLZUYOUXPUXQUYSUYTUWJUXPUXQUYSUYTUXQUYSMUXPUYBCLZUYTUWHCU
      YBVFUXOUWEVUAUYTUWCUXOUWEMVUAUYTUWTUWBUWEVUAUYTSUWTUWBUWEURVUAUYTUYBABCVG
      VHVNVIVJVKVLVMVOVPVQVRVSVTUYFBIUDUWHUYCUXRWEUQWAUXKUYLUCUYFUXLUXBUYFPZUXD
      UYHUXJUYKVUBUXCUYGUXAUXBUYFWBWCVUBUXGHUXIUYJVUBUXHUYIQUXBUYFWDWFWGWHWIWJU
      XSUYHUYKUWPUXSUXAIUWHUYCWKZUYGUXSUXAUYAUWITZVUCUXSUXAUYAUWGTZVUDUXPUWJUXA
      VUEPZUXQUWCVUFUXOUWEUWCAWLZDPZAWMZEPZVUFUWCADWNVUHDEAWODAWPWJDEAWQVUHVUJM
      ZDUYAETZUXAVUEVUKUYAVUITZVUGVULDVUMVUGPVUKAWRXGVUKVUIEUYAVUHVUJWSWTVUHVUJ
      XAXBFEUWGUYAGXCXDXEXFXHUWJUXPVUEVUDPUXQUWGUWIUYAXIXJXRIUYAUWHXKXLUXSUYCXM
      LZIUWHULVUCUYGPUXSVUNIUWHUXSVUNUYSUXPUWJVUNUXQUWEVUNUXOUWCUWEUYAXMLVUNAUW
      DXNUYAUYBXMXOWJXPXHXQXSIUDUWHUYCXMXTWJXRUXSUXGUWPHUYJUXEUYJLZUXEUYFUOZUXE
      QLZMZUXSUXGUWPSVUOUXEUYILZVUQMVURUXEUYIQYAVUSVUPVUQUXEUYFHUPZUQYBYCUXSVUR
      UXGUWPUXSVURUXGURZJOZAKOZTZPZKUXERZJUTZUWOLZUWGVVGNZPZUWPVVAVVGUWHUOZVVGQ
      LZVVHVVAUGVVGUWHUYNVVGLUYNVVDPZKUXERZVVAUYNUWHLZVVFVVNJUYNUYRVVBUYNPVVEVV
      MKUXEVVBUYNVVDVAVCVDVVAVVMVVOKUXEUXSVURVVCUXELZVVMVVOSZSZUXGUXSVUPVVRVUQU
      XPUWJVUPVVRUXQUXPUWJVUPVVRVUPVVPUXPUWJVVQVUPVVPVVCUYFLZUXPUWJVVQSSZUXEUYF
      VVCYDVVSVVCUYCPZIUWHRZVVTUYEVWBUDVVCKUPUXTVVCPUYDVWAIUWHUXTVVCUYCVAVCVDVW
      AVVTIUWHVWAVVDAUYCTZPZUYSVVTVVCUYCAXIVVMVWDUXPUWJUYSVVOVVMVWDUYNVWCPZUXPU
      WJUYSVVOSZSZSVVMVWDVWEUYNVVDVWCYEVHUXPVWEVWGUWCVWEVWGSUXOUWEUWJUYSUWCVWEV
      VOUWJUYSUYBUWGUOZUWCVWEVVOSSUYSVWHUWJUYBUWIUOUYBUWHYFUWGUWIUYBYIYGVWEVWHU
      WCUYSVVOUWCVWHVWEVWFUWCVWHVWEVWFSUWCVWHMZVWEUYNUYBPZVWFVWIUYBVWCUYNVWHUWC
      UYBEUOZUYBVWCPEUWGUYBGYHUWCVWKMVWCUYBDEUYBAYJYKYLWCVWJVVOUYSUYNUYBUWHYMYN
      YOVHYPUUAUUBYQXFUUCYSUUDUUJUUEUUFYSYQUUGUUHUUIVOVRVSVTUXSVURVVLUXGUXPUWJU
      XQVURVVLUXPUXQVURVVLSZSUWJUXPVWLUXQUWEVWLUXOUWCUWEVUQVVLVUPUWEVUQVVLUWEVU
      QMZVVGVVPVVEMKJUUKZWMZQKJUXEVVDUULVWMVWOUXEUUMUUNZVWOQLZUWEUXEVWOVWNUFZVU
      QVWPUWEVWNUXEWNZVWRUWEVVDXMLZKUXEULZVWSUWEVWTKUXEUWEVWTVVPAVVCUWDXOZXQXSK
      JUXEVVDVWNVWNUUOUVBYTUXEVWNUUPYTVUQVWRVWPUXEVWOVWNUUQUURUUSVUQVWPVWQUWEUX
      EVWOUUTUVAUVCUVDVHUVEXPXQXQUVFVOVVHVVGUWNLZVVLMVVKVVLMVVGUWNQYAVXCVVKVVLV
      VGUWHKJUXEVVDVUTWEUQYBYCYRVVAUWGKUXEVVDWKZVVIVVAUWGAUXFTZVXDVVAUWGAUXATZV
      XEUXSVURUWGVXFPZUXGUXPUWJVXGUXQUWCVXGUXOUWEUWCADTZEVXFUWGDEAUVGVXHVXFPUWC
      DUXAAFXCXGEUWGPUWCGXGXBXFXHXHUXGUXSVXFVXEPVURUXAUXFAXIUVHXRKAUXEXKXLVVAVX
      AVXDVVIPVVAVWTKUXEVVAVWTVVPUXSVURVWTUXGUXPUWJVWTUXQUWEVWTUXOUWCVXBXPXHXHX
      QXSKJUXEVVDXMXTWJXRUWMVVJUBVVGUWOUWKVVGPUWLVVIUWGUWKVVGWBWCUVIXEVQVSVRUVL
      UVJVQUVMUVKUVNUVOUVPUVQVSUVRUVSUVTUAUBCUNYR $.
      $( [1-Dec-2010] $) $( [12-Jul-2009] $)
  $}

  ${
    $d f J $.  $d f K $.
    $( Compactness is a topological property-that is, for any two homeomorphic
       topologies, either both are compact or neither is.  (Contributed by Jeff
       Hankins, 30-Jun-2009.) $)
    comptoppr $p |- ( ( J e. Top /\ K e. Top /\ J ~= K ) -> ( J e. Comp <-> K
    e. Comp ) ) $=
      ( vf ctop wcel ccomp wb wa co cuni wf1o ccn cvv wi wfo f1ofo cncomp exp43
      eqid com4l chomeo wbr cv chomeosm wex hmph ccnv visset hmeobc mp3an3 syl5
      w3a adantll 3adantr3 f1ocnv syl adantlr 3adantr2 impbid ex sylbid exlimdv
      imp32 3impia ) ADEZBDEZABUAUBZAFEZBFEZGZVEVFHZVGCUCZABUDIEZCUEVJCABUFVKVM
      VJCVKVMAJZBJZVLKZVLABLIEZVLUGZBALIEZULZVJVEVFVLMEVMVTGCUHMVLABVNVOVNSZVOS
      ZUIUJVKVTVJVKVTHVHVIVKVPVQVHVINZVSVFVPVQHWCVEVFVPVQWCVPVNVOVLOZVFVQWCNVNV
      OVLPVHVFWDVQVIVHVFWDVQVIVLABVNVOWAWBQRTUKVCUMUNVKVPVSVIVHNZVQVEVPVSHWEVFV
      EVPVSWEVPVOVNVROZVEVSWENVPVOVNVRKWFVNVOVLUOVOVNVRPUPVIVEWFVSVHVIVEWFVSVHV
      RBAVOVNWBWAQRTUKVCUQURUSUTVAVBVAVD $.
      $( [1-Dec-2010] $) $( [30-Jun-2009] $)
  $}

  ${
    $d J y z $.
    $( A finite topology is compact.  (Contributed by FL, 22-Dec-2008.) $)
    fintopcomp $p |- ( J e. ( Top i^i Fin ) -> J e. Comp ) $=
      ( vy vz ctop cfn cin wcel cuni cv wceq cpw wrex wi wral ccomp inss1 sseli
      inss2 wa visset pwid wss elpw ssfi sylan2b unieq eqeq2d rcla4ev ex sylbir
      elin sylancr ralrimiva syl iscomp sylanbrc ) ADEFZGZADGAHZBIZHZJZUSCIZHZJ
      ZCUTKZEFZLZMZBAKZNZAOGUQDADEPQURAEGZVKUQEADERQVLVIBVJVLUTVJGZSUTVFGZUTEGZ
      VIUTBTZUAVMVLUTAUBVOUTAVPUCAUTUDUEVNVOSUTVGGZVIUTVFEUKVQVBVHVEVBCUTVGVCUT
      JVDVAUSVCUTUFUGUHUIUJULUMUNBCAUOUP $.
      $( [13-Jun-2011] $)
  $}


