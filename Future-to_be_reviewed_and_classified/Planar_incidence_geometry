$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Planar incidence geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Plig $.

  $( Extend class notation with the class of all planar incidence
     geometries. $)
  cplig $a class Plig $.

  ${
    $d x a b l c $.
    $( Planar incidence geometry.  I use Hilbert's "axioms" adapted to planar
       geometry. ` e. ` is the incidence relation.  I could take a generic
       incidence relation but I'm lazy and I'm not sure the gain is worth the
       extra work.  Much of what follows is directly borrowed from Aitken.
       ~ http://public.csusm.edu/aitken_html/m410/betweenness.pdf (Contributed
       by FL, 2-Aug-2009.) $)
    df-plig $a |- Plig = { x | ( A. a e. U. x A. b e. U. x
          ( a =/= b -> E! l e. x ( a e. l /\ b e. l ) )
      /\ A. l e. x E. a e. U. x E. b e. U. x ( a =/= b /\ a e. l /\ b e. l )
      /\ E. a e. U. x E. b e. U. x E. c e. U. x A. l e. x
           -. ( a e. l /\ b e. l /\ c e. l ) ) } $.
  $}

  ${
    $d L a b c l x $.  $d P a b c x $.
    isplig.1 $e |- P = U. L $.
    $( The predicate "is a planar incidence geometry".  (Contributed by FL,
       2-Aug-2009.) $)
    isplig $p |- ( L e. A -> ( L e. Plig <->
     ( A. a e. P A. b e. P ( a =/= b -> E! l e. L ( a e. l /\ b e. l ) )
     /\ A. l e. L E. a e. P E. b e. P ( a =/= b /\ a e. l /\ b e. l )
     /\ E. a e. P E. b e. P E. c e. P A. l e. L
           -. ( a e. l /\ b e. l /\ c e. l ) ) ) ) $=
      ( vx cv wne wcel wreu wi cuni wral w3a wrex raleqbidv rexeqbidv wa reueq1
      cplig wceq unieq syl6eqr imbi2d rexeqdv raleqbi1dv raleq 3anbi123d elab2g
      wn df-plig ) DJZEJZKZUOGJZLZUPURLZUAZGIJZMZNZEVBOZPZDVEPZUQUSUTQZEVERZDVE
      RZGVBPZUSUTFJURLQUMZGVBPZFVERZEVERZDVERZQUQVAGCMZNZEBPZDBPZVHEBRZDBRZGCPZ
      VLGCPZFBRZEBRZDBRZQICUCAVBCUDZVGVTVKWCVPWGWHVFVSDVEBWHVECOBVBCUEHUFZWHVDV
      REVEBWIWHVCVQUQVAGVBCUBUGSSVJWBGVBCWHVIWADVEBWIWHVHEVEBWIUHTUIWHVOWFDVEBW
      IWHVNWEEVEBWIWHVMWDFVEBWIVLGVBCUJTTTUKIDEFGUNUL $.
      $( [1-Dec-2010] $) $( [2-Aug-2009] $)
  $}

  ${
    $d L a b c l $.  $d P a b c $.
    tncp.1 $e |- P = U. L $.
    $( There exist three non colinear points.  (Contributed by FL,
       3-Aug-2009.) $)
    tncp $p |- ( L e. Plig -> E. a e. P E. b e. P E. c e. P A. l e. L
           -. ( a e. l /\ b e. l /\ c e. l ) ) $=
      ( cplig wcel cv wne wa wreu wi wral w3a wrex wn isplig ibi simp3d ) BHIZC
      JZDJZKZUCFJZIZUDUFIZLFBMNDAOCAOZUEUGUHPDAQCAQFBOZUGUHEJUFIPRFBOEAQDAQCAQZ
      UBUIUJUKPHABCDEFGSTUA $.
      $( [1-Dec-2010] $) $( [2-Aug-2009] $)
  $}

  ${
    $d a b c d l G $.  $d a b c d l L $.  $d a b c d l P $.
    lpni.1 $e |- P = U. G $.
    $( For any line, there exists a point not on the line.  (Contributed by
       Jeff Hankins, 15-Aug-2009.) $)
    lpni $p |- ( ( G e. Plig /\ L e. G ) -> E. a e. P a e/ L ) $=
      ( vb vl vc vd wcel cv wrex w3a wn wceq eleq2 notbid eleq1 rcla4ev ex wnel
      cplig wral wi wa 3anbi123d rcla4cv w3o 3jaao 3ianor df-nel rexbii 3imtr4g
      tncp syl9r 3expia r19.23adv r19.23aivv syl imp ) BUBJZCBJZDKZCUAZDALZVAFK
      ZGKZJZHKZVGJZIKZVGJZMZNZGBUCZIALZHALFALVBVEUDZABFHIGEUNVPVQFHAAVFAJZVIAJZ
      UEVOVQIAVRVSVKAJZVOVQUDVOVBVFCJZVICJZVKCJZMZNZVRVSVTMZVEVNWEGCBVGCOZVMWDW
      GVHWAVJWBVLWCVGCVFPVGCVIPVGCVKPUFQUGWFWANZWBNZWCNZUHVCCJZNZDALZWEVEVRWHWM
      VSWIVTWJVRWHWMWLWHDVFAVCVFOWKWAVCVFCRQSTVSWIWMWLWIDVIAVCVIOWKWBVCVICRQSTV
      TWJWMWLWJDVKAVCVKOWKWCVCVKCRQSTUIWAWBWCUJVDWLDAVCCUKULUMUOUPUQURUSUT $.
      $( [1-Dec-2010] $) $( [3-Aug-2009] $)
  $}


