$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Planar incidence geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Plig $.

  $( Extend class notation with the class of all planar incidence
     geometries. $)
  cplig $a class Plig $.

  ${
    $d x a b l c $.
    $( Planar incidence geometry.  I use Hilbert's "axioms" adapted to planar
       geometry. ` e. ` is the incidence relation.  I could take a generic
       incidence relation but I'm lazy and I'm not sure the gain is worth the
       extra work.  Much of what follows is directly borrowed from Aitken.
       ~ http://public.csusm.edu/aitken_html/m410/betweenness.pdf (Contributed
       by FL, 2-Aug-2009.) $)
    df-plig $a |- Plig = { x | ( A. a e. U. x A. b e. U. x
          ( a =/= b -> E! l e. x ( a e. l /\ b e. l ) )
      /\ A. l e. x E. a e. U. x E. b e. U. x ( a =/= b /\ a e. l /\ b e. l )
      /\ E. a e. U. x E. b e. U. x E. c e. U. x A. l e. x
           -. ( a e. l /\ b e. l /\ c e. l ) ) } $.
  $}

  ${
    $d L a b c l x $.  $d P a b c x $.
    isplig.1 $e |- P = U. L $.
    $( The predicate "is a planar incidence geometry".  (Contributed by FL,
       2-Aug-2009.) $)
    isplig $p |- ( L e. A -> ( L e. Plig <->
     ( A. a e. P A. b e. P ( a =/= b -> E! l e. L ( a e. l /\ b e. l ) )
     /\ A. l e. L E. a e. P E. b e. P ( a =/= b /\ a e. l /\ b e. l )
     /\ E. a e. P E. b e. P E. c e. P A. l e. L
           -. ( a e. l /\ b e. l /\ c e. l ) ) ) ) $=
      ( vx cv wne wcel wa wreu wi cuni wral w3a wrex wn cplig wceq unieq
      syl6eqr reueq1 imbi2d raleqbidv rexeqdv rexeqbidv raleqbi1dv raleq
      3anbi123d df-plig elab2g ) DJZEJZKZUOGJZLZUPURLZMZGIJZNZOZEVBPZQZDVEQZUQU
      SUTRZEVESZDVESZGVBQZUSUTFJURLRTZGVBQZFVESZEVESZDVESZRUQVAGCNZOZEBQZDBQZVH
      EBSZDBSZGCQZVLGCQZFBSZEBSZDBSZRICUAAVBCUBZVGVTVKWCVPWGWHVFVSDVEBWHVECPBVB
      CUCHUDZWHVDVREVEBWIWHVCVQUQVAGVBCUEUFUGUGVJWBGVBCWHVIWADVEBWIWHVHEVEBWIUH
      UIUJWHVOWFDVEBWIWHVNWEEVEBWIWHVMWDFVEBWIVLGVBCUKUIUIUIULIDEFGUMUN $.
      $( [1-Dec-2010] $) $( [2-Aug-2009] $)
  $}

  ${
    $d L a b c l $.  $d P a b c $.
    tncp.1 $e |- P = U. L $.
    $( There exist three non colinear points.  (Contributed by FL,
       3-Aug-2009.) $)
    tncp $p |- ( L e. Plig -> E. a e. P E. b e. P E. c e. P A. l e. L
           -. ( a e. l /\ b e. l /\ c e. l ) ) $=
      ( cplig wcel cv wne wa wreu wi wral w3a wrex wn isplig ibi 3simp3d ) BHIZ
      CJZDJZKZUCFJZIZUDUFIZLFBMNDAOCAOZUEUGUHPDAQCAQFBOZUGUHEJUFIPRFBOEAQDAQCAQ
      ZUBUIUJUKPHABCDEFGSTUA $.
      $( [1-Dec-2010] $) $( [2-Aug-2009] $)
  $}

  ${
    $d a b c d l G $.  $d a b c d l L $.  $d a b c d l P $.
    lpni.1 $e |- P = U. G $.
    $( For any line, there exists a point not on the line.  (Contributed by
       Jeff Hankins, 15-Aug-2009.) $)
    lpni $p |- ( ( G e. Plig /\ L e. G ) -> E. a e. P a e/ L ) $=
      ( vb vl vc vd cplig wcel cv wnel wrex w3a wn wral wi tncp wa wceq eleq2
      3anbi123d notbid rcla4cv w3o eleq1 rcla4ev ex 3jaao 3ianor df-nel rexbii
      3imtr4g syl9r 3expia r19.23adv r19.23aivv syl imp ) BJKZCBKZDLZCMZDANZVAF
      LZGLZKZHLZVGKZILZVGKZOZPZGBQZIANZHANFANVBVERZABFHIGESVPVQFHAAVFAKZVIAKZTV
      OVQIAVRVSVKAKZVOVQRVOVBVFCKZVICKZVKCKZOZPZVRVSVTOZVEVNWEGCBVGCUAZVMWDWGVH
      WAVJWBVLWCVGCVFUBVGCVIUBVGCVKUBUCUDUEWFWAPZWBPZWCPZUFVCCKZPZDANZWEVEVRWHW
      MVSWIVTWJVRWHWMWLWHDVFAVCVFUAWKWAVCVFCUGUDUHUIVSWIWMWLWIDVIAVCVIUAWKWBVCV
      ICUGUDUHUIVTWJWMWLWJDVKAVCVKUAWKWCVCVKCUGUDUHUIUJWAWBWCUKVDWLDAVCCULUMUNU
      OUPUQURUSUT $.
      $( [1-Dec-2010] $) $( [3-Aug-2009] $)
  $}


