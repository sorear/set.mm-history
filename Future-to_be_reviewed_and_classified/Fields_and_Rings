$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Fields and Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Com2 $.

  $( Extend class notation with a class that adds commutativity to various
     flavors of rings. $)
  ccm2 $a class Com2 $.


  ${
    $d g h a b $.
    $( A device to add commutativity to various sorts of rings.  I use
       ` ran g ` because I suppose ` g ` has a neutral element and therefore is
       onto. $)
    df-com2 $a |- Com2 = { <. g , h >. | A. a e. ran g A. b e. ran g
     ( a h b ) = ( b h a ) } $.
  $}

  ${
    $d G a b x y $.  $d H a b x y $.
    $( A device to add commutativity to various sorts of rings.  (Contributed
       by FL, 6-Sep-2009.) $)
    iscom2 $p |- ( ( G e. A /\ H e. B ) -> ( <. G , H >. e. Com2 <->
     A. a e. ran G A. b e. ran G ( a H b ) = ( b H a ) ) ) $=
      ( vy vx wcel wa cop ccm2 cv co wceq crn wral copab df-com2 a1i eleq2d
      rneq raleqdv raleqbidv eqidd opreq eqeq12d opelopabg bitrd ) CAIDBIJZCDKZ
      LIUKEMZFMZGMZNZUMULUNNZOZFHMZPZQZEUSQZHGRZIULUMDNZUMULDNZOZFCPZQZEVFQZUJL
      VBUKLVBOUJHGEFSTUAVAUQFVFQZEVFQVHHGCDABURCOZUTVIEUSVFURCUBZVJUQFUSVFVKUCU
      DUNDOZVIVGEVFVFVLVFUEZVLUQVEFVFVFVMVLUOVCUPVDULUMUNDUFUMULUNDUFUGUDUDUHUI
      $.
      $( [6-Sep-2009] $)
  $}

  $c Fld $.

  $( Extend class notation with the class of all fields. $)
  cfld $a class Fld $.

  $( Add alternate definition. Add Kennington's one (=/= the zero ring),
       and Mizar's one: ` ( Id `` G ) =/= ( Id `` H ) ` $)
  $( Definition of a field.  A field is a commutative division ring. $)
  df-fld $a |- Fld = ( DivRingOps i^i Com2 ) $.

  ${
    $d v w x y z $.
    $( The class of all unital rings is a relation.  (Contributed by FL,
       31-Aug-2009.) $)
    relrng $p |- Rel RingOps $=
      ( vw vv vx vy vz cring wrel cv cabl wcel crn cxp wf wa co wceq w3a wral
      wrex copab relopab df-ringo releqi mpbir ) FGAHZIJUEKZUFLUFBHZMNCHZDHZUGO
      ZEHZUGOUHUIUKUGOZUGOPUHUIUKUEOUGOUJUHUKUGOZUEOPUHUIUEOUKUGOUMULUEOPQEUFRD
      UFRCUFRUIUHUGOUIPUJUIPNDUFRCUFSNNZABTZGUNABUAFUOCDEABUBUCUD $.
      $( [31-Aug-2009] $)
  $}

  ${
    rngn0.1 $e |- G = ( 1st ` R ) $.
    rngn0.2 $e |- X = ran G $.
    $( The base set of a ring is not empty.  (Contributed by FL,
       24-Jan-2010.) $)
    rngn0 $p |- ( R e. RingOps -> X =/= (/) ) $=
      ( cring wcel cgr c0 wne ringogrpo grpon0 syl ) AFGBHGCIJABDKBCELM $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d G u x z $.  $d G u y z $.  $d X u x z $.  $d X u y z $.
    $( The range of an internal operation with a left and right identity
       element equals its base set.  (Contributed by FL, 24-Jan-2010.) $)
    rngmgmbs4 $p |- ( ( G : ( X X. X ) --> X /\
      E. u e. X A. x e. X ( ( x G u ) = x /\ ( u G x ) = x ) )
         -> ran G = X ) $=
      ( vz vy cxp wf cv co wceq wa wral wrex wfo crn wcel opreq2 id eqeq12d
      rcla4v eqeq1d eqcom syl5bb rcla4ev ex syld simpr ralimi syl5 reximdv
      impcom r19.21aiva anim2i fooprv sylibr forn syl ) DDGZDCHZAIZBIZCJVAKZVBV
      ACJZVAKZLZADMZBDNZLZUSDCOZCPDKVIUTEIZVBFIZCJZKZFDNZBDNZEDMZLVJVHVQUTVHVPE
      DVKDQZVHVPVRVGVOBDVRVEADMZVOVGVRVSVBVKCJZVKKZVOVEWAAVKDVAVKKZVDVTVAVKVAVK
      VBCRWBSTUAVRWAVOVNWAFVKDVLVKKZVMVKKWAVNWCVMVTVKVLVKVBCRUBVKVMUCUDUEUFUGVF
      VEADVCVEUHUIUJUKULUMUNBFEDDDCUOUPUSDCUQUR $.
      $( [24-Jan-2010] $)
  $}

  ${
    rnplrnml0.1 $e |- H = ( 2nd ` R ) $.
    rnplrnml0.2 $e |- G = ( 1st ` R ) $.
    $( In a unital ring the domain of the first variable of the addition equals
       the domain of the first variable of the multiplication.  (Contributed by
       FL, 24-Jan-2010.) $)
    rnplrnml0 $p |- ( R e. RingOps -> dom dom G = dom dom H ) $=
      ( cring wcel crn cxp wf cdm wceq eqid ringsm cgr wfo wi ringogrpo grpfo
      fof fdm wa eqtr dmeqd expcom eqcoms syl com12 3syl mpd ) AFGZBHZULIZULCJZ
      BKZKCKZKLZABCULEDULMZNUKBOGUMULBPZUNUQQZABERBULURSUSUMULBJUOUMLZUTUMULBTU
      MULBUAUNVAUQUNUPUMLVAUQQZUMULCUAVBUMUPVAUMUPLZUQVAVCUBUOUPUOUMUPUCUDUEUFU
      GUHUIUIUJ $.
      $( [24-Jan-2010] $)

    $( In a unital ring the range of the addition equals the domain of the
       first variable of the multiplication.  (Contributed by FL,
       24-Jan-2010.) $)
    rnplrnml2 $p |- ( R e. RingOps -> ran G = dom dom H ) $=
      ( cring wcel crn cdm cgr wceq ringogrpo grprndm syl rnplrnml0 eqtrd ) AFG
      ZBHZBIIZCIIQBJGRSKABELBMNABCDEOP $.
      $( [24-Jan-2010] $)

    ${
      $d G x y z $.  $d H x y z $.
      $( In a unital ring the range of the addition equals the range of the
         multiplication.  (Contributed by FL, 24-Jan-2010.) $)
      rnplrnml $p |- ( R e. RingOps -> ran G = ran H ) $=
        ( vx vy vz cring wcel crn cxp wf cv co wceq wa wral wrex eqid ringsm
        cabl w3a ringoi simprr syl rngmgmbs4 syl11anc eqcomd ) AIJZCKZBKZUJULUL
        LULCMZFNZGNZCOUNPUOUNCOZUNPQFULRGULSZUKULPABCULEDULTZUAUJBUBJUMQZUPHNZC
        OUOUNUTCOZCOPUOUNUTBOCOUPUOUTCOZBOPUOUNBOUTCOVBVABOPUCHULRFULRGULRZUQQQ
        UQGFHABCULEDURUDUSVCUQUEUFFGCULUGUHUI $.
        $( [24-Jan-2010] $)
    $}
  $}

  ${
    $d H w x y $.  $d H x y z $.  $d R w x y $.  $d R x y z $.
    unmnd.1 $e |- H = ( 2nd ` R ) $.
    $( In a unital ring the multiplication is a monoid.  (Contributed by FL,
       24-Jan-2010.) $)
    unmnd $p |- ( R e. RingOps -> H e. Mnd ) $=
      ( vx vy vz vw cring wcel cdm cxp wf cv co wceq wral wa wrex w3a cmnd
      c1st cfv crn eqid ringsm wi ringoass expcom 3exp com34 imp impcom
      r19.21aiv ex r19.21aivv cabl ringoi simprd 3jca wb rnplrnml2 xpid11
      biimpri feq23 mpancom eleq2 imbi1d ralbidv2 imbi12d anbi12d rexbidv2
      3anbi123d eqcoms syl mpbird c2nd cvv fvex eleq1 mpbiri ismnd1 ax-mp
      sylibr ) AHIZBJJZWEKZWEBLZDMZEMZBNZFMZBNWHWIWKBNBNOZFWEPZEWEPZDWEPZWIWHBN
      WIOWJWIOQZEWEPZDWERZSZBTIZWDWSAUAUBZUCZXBKZXBBLZWLFXBPZEXBPZDXBPZWPEXBPZD
      XBRZSZWDXDXGXIAXABXBXAUDZCXBUDZUEWDXEDEXBXBWDWHXBIZWIXBIZQZXEWDXOQWLFXBXO
      WDWKXBIZWLUFZXMXNWDXQUFXMXNXPWDWLXMXNXPWDWLUFWDXMXNXPSWLWHWIWKAXABXBXKCXL
      UGUHUIUJUKULUMUNUOWDWJGMZBNWHWIXRBNZBNOWHWIXRXANBNWJWHXRBNZXANOWHWIXANXRB
      NXTXSXANOSGXBPEXBPDXBPZXIWDXAUPIXDQYAXIQDEGAXABXBXKCXLUQURURUSWDXBWEOWSXJ
      UTZAXABCXKVAYBWEXBWEXBOZWGXDWOXGWRXIWFXCOZYCWGXDUTYDYCWEXBVBVCWFWEXCXBBVD
      VEYCWNXFDWEXBYCWHWEIZXMWNXFWEXBWHVFZYCWMXEEWEXBYCWIWEIZXNWMXEWEXBWIVFZYCW
      LWLFWEXBYCWKWEIXPWLWEXBWKVFVGVHVIVHVIVHYCWQXHDWEXBYCYEXMWQXHYFYCWPWPEWEXB
      YCYGXNWPYHVGVHVJVKVLVMVNVOBAVPUBZOZWTWSUTZCYJBVQIZYKYJYLYIVQIAVPVRBYIVQVS
      VTDEFVQBWEWEUDWAVNWBWC $.
      $( [24-Jan-2010] $)
  $}

  ${
    fora1.1 $e |- G e. A $.
    $( In a unital ring the addition is an abelian group.  (Contributed by FL,
       31-Aug-2009.) $)
    fora1 $p |- ( <. G , H >. e. RingOps -> G e. AbelOp ) $=
      ( cop c1st cfv elisseti op1st eqcomi ringoablo ) BCEZBLFGBBCBADHIJK $.
      $( [31-Aug-2009] $)
  $}

  $( In a unital ring the addition is an abelian group.  (Contributed by FL,
     31-Aug-2009.) $)
  fora2 $p |- ( ( G e. A /\ <. G , H >. e. RingOps ) -> G e. AbelOp ) $=
    ( wcel cop cring cabl cvv wi elisset csn cif wceq opeq1 eleq1d eleq1
    imbi12d snex elimel fora1 dedth syl imp ) BADZBCEZFDZBGDZUDBHDZUFUGIZBAJUHU
    IUHBAAEAEZKZLZCEZFDZULGDZIBUKBULMZUFUNUGUOUPUEUMFBULCNOBULGPQHULCBUKHUJRSTU
    AUBUC $.
    $( [31-Aug-2009] $)

  $( In a unital ring the addition is an abelian group.  (Contributed by FL,
     31-Aug-2009.) $)
  fora $p |- ( <. G , H >. e. RingOps -> G e. AbelOp ) $=
    ( cvv wcel cop cring cabl wbr df-br relrng brrelexi sylbir fora2 mpancom )
    ACDZABEFDZAGDPABFHOABFIABFJKLCABMN $.
    $( [31-Aug-2009] $)

  ${
    uridm.1 $e |- H = ( 2nd ` R ) $.
    uridm.2 $e |- X = ran ( 1st ` R ) $.
    ${
      uridm.3 $e |- U = ( Id ` H ) $.
      $( The unit of a ring is an identity element for the multiplication.
         (Contributed by FL, 18-Feb-2010.) $)
      ringoidmlem $p |- ( ( R e. RingOps /\ A e. X ) ->
        ( ( U H A ) = A /\ ( A H U ) = A ) ) $=
        ( cring wcel co wceq wa wi crn cmnd cmagm cexid cin unmnd mndmgmid
        eqid cmpidelt ex 3syl c1st cfv wb rnplrnml eqtr simpl eleq2d imbi1d
        syl mpan mpcom mpbird imp ) BIJZAEJZCADKALACDKALMZUSUTVANZADOZJZVANZUSD
        PJDQRSJZVEBDFTDUAVFVDVAACDVCVCUBHUCUDUEBUFUGZOZVCLZUSVBVEUHZBVGDFVGUBUI
        EVHLZVIUSVJNZGVKVIMEVCLZVLEVHVCUJVMUSVJVMUSMZUTVDVAVNEVCAVMUSUKULUMUDUN
        UOUPUQUR $.
        $( [18-Feb-2010] $)

      $( The unit of a ring is an identity element for the multiplication.
         (Contributed by FL, 18-Apr-2010.) $)
      ringolidm $p |- ( ( R e. RingOps /\ A e. X ) -> ( U H A ) = A ) $=
        ( cring wcel wa co wceq ringoidmlem simpld ) BIJAEJKCADLAMACDLAMABCDEFG
        HNO $.
        $( [18-Apr-2010] $)
    $}

    ${
      uridm2.2 $e |- U = ( Id ` H ) $.
      $( The unit of a ring is an identity element for the multiplication.
         (Contributed by FL, 18-Apr-2010.) $)
      ringoridm $p |- ( ( R e. RingOps /\ A e. X ) -> ( A H U ) = A ) $=
        ( cring wcel wa co wceq ringoidmlem simprd ) BIJAEJKCADLAMACDLAMABCDEFG
        HNO $.
        $( [18-Apr-2010] $)
    $}
  $}

  ${
    $d A x $.  $d R x $.  $d X x $.
    on1el3.1 $e |- G = ( 1st ` R ) $.
    on1el3.2 $e |- X = ran G $.
    $( The only unital ring with a base set consisting in one element is the
       zero ring.  (Contributed by FL, 13-Feb-2010.) $)
    on1el3 $p |- ( ( R e. RingOps /\ A e. B ) -> ( X = { A } <-> R =
       <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ) ) $=
      ( vx wcel cring csn wceq cop wb cv wi sneq eqeq2d opeq12 anidms mpancom
      sneqd opeq12d bibi12d imbi2d cxp c1st cfv wf cgr wfo eqid ringogrpo crn
      rneqi eqtri grpfo fof 3syl c2nd ringsm w3a cvv wa feq23 visset xpsn feq2
      fconst2 biimpi opex syl6eq syl6bi ax-mp anim12d ex xpid11 syl5ib pm2.43i
      exp3a com13 3imp a1i cdm wrel wss relrng relssdmrn xpss sstri sseli
      jctild eqopi syl6 3expd mpd syl5ibr fveq2 snex op1st eqtr mpan2 rneq
      syl5eq eqcomi rnsnop 3eqtr3g impbid1 vtoclg impcom ) ABICJIZEAKZLZCAAMZAM
      ZKZXPMZLZNZXKEHOZKZLZCXTXTMZXTMZKZYEMZLZNZPXKXSPHABXTALZYHXSXKYIYBXMYGXRY
      IYAXLEXTAQRYIYFXQCYIYEXPYEXPYIYDXOYCXNLZYIYDXOLYIYJXTXTAASTYCXTXNASUAUBZY
      KUCRUDUEXKYBYGXKEEUFZYAYAUFZLZYGYBXKYLECUGUHZUIZYNYGPZXKYOUJIYLEYOUKYPCYO
      YOULUMYOEEDUNZYOUNZGDYOFUOUPUQYLEYOURUSXKYLECUTUHZUIZYPYQPCDYTEFYTULGVAXK
      UUAYPYNYGXKUUAYPYNVBZCVCVCUFZIZYOYELZYTYELZVDZVDYGXKUUBUUGUUDUUBUUGPXKUUA
      YPYNUUGYNYPUUAUUGYNYPUUAUUGYNYPUUAVDUUGPZYNYBUUHYNYNYBUUHYNYBVDZYPUUEUUAU
      UFUUIYPYMYAYOUIZUUEYLEYMYAYOVEYMYCKZLZUUJUUEPXTXTHVFZUUMVGZUULUUJUUKYAYOU
      IZUUEYMUUKYAYOVHUUOYOUUKYAUFZYEUUOYOUUPLUUKXTYOUUMVIVJYCXTXTXTVKZUUMVGZVL
      VMVNVMUUIUUAYMYAYTUIZUUFYLEYMYAYTVEUULUUSUUFPUUNUULUUSUUKYAYTUIZUUFYMUUKY
      AYTVHUUTYTUUPYEUUTYTUUPLUUKXTYTUUMVIVJUURVLVMVNVMVOVPEYAVQZVRVSVTWAWBWCJU
      UCCJJWDZJUNZUFZUUCJWEJUVDWFWGJWHVNUVBUVCWIWJWKWLCYEYEWMWNWOWPWPUVAWQYGYOY
      FUGUHZLZUUEYBCYFUGWRUVFUVEYELUUEYEYEYDWSWTYOUVEYEXAXBUUEYSYEUNEYAYOYEXCEY
      SDYOLZEYSLFUVGYRYSEDYOXCGXDVNXEYCXTUUQUUMXFXGUSXHXIXJ $.
      $( [13-Feb-2010] $)

    ${
      $d A x $.  $d R x $.  $d X x $.
      $( The only unital ring with one element is the zero ring.  (Contributed
         by FL, 14-Feb-2010.) $)
      on1el4 $p |- ( ( R e. RingOps /\ A e. X ) -> ( X ~~ 1o <-> R =
       <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ) ) $=
        ( vx wcel cring c1o cen wbr cop csn wceq wb cv wi opeq12 anidms
        mpancom sneqd opeq12d eqeq2d bibi2d imbi2d wa en1eqsn on1el3 biimpd ex
        com13 syl pm2.43a com23 imp c1st cfv fveq2 snex op1st eqtr crn rneq
        opex visset rnsnop ensn1 breq1 mpbiri a1d sylancl sylancr ax-mp com12
        adantr impbid vtoclga impcom ) ADHBIHZDJKLZBAAMZAMZNZWDMZOZPZVTWABGQZWH
        MZWHMZNZWKMZOZPZRVTWGRGADWHAOZWNWGVTWOWMWFWAWOWLWEBWOWKWDWKWDWOWJWCWIWB
        OZWOWJWCOWOWPWHWHAASTWIWHWBASUAUBZWQUCUDUEUFWHDHZVTWNWRVTUGWAWMWRVTWAWM
        RWRWAVTWMWAWRVTWMRZWRWAWRWSRZWRWAUGDWHNZOZWTWHDUHVTWRXBWMVTWRXBWMRVTWRU
        GXBWMWHDBCDEFUIUJUKULUMUKUNUOUPWRWMWARVTWMWRWAWMBUQURZWLUQURZOZXDWKOZWR
        WARZBWLUQUSWKWKWJUTVAXEXFUGXCWKOZXGXCXDWKVBXHXCVCZWKVCZOZXGXCWKVDCXCOZX
        KXGRZEXLDCVCZOZXNXIOZXMFCXCVDXOXPUGDXIOZXMDXNXIVBXQXKXGXQXKUGDXJOZXJXAO
        ZXGDXIXJVBWIWHWHWHVEGVFZVGXRXSUGXBXGDXJXAVBXBWAWRXBWAXAJKLWHXTVHDXAJKVI
        VJVKUMVLUKUMVMVNUMUMVLVOVPVQUKVRVS $.
        $( [14-Feb-2010] $)
    $}

    ${
      on1el3.3 $e |- Z = ( Id ` G ) $.
      $( The only unital ring with one element is the zero ring.  (Contributed
         by FL, 15-Feb-2010.) $)
      on1el6 $p |- ( R e. RingOps -> ( X ~~ 1o <-> R =
        <. { <. <. Z , Z >. , Z >. } , { <. <. Z , Z >. , Z >. } >. ) ) $=
        ( cring wcel c1o cen wbr cop csn wceq wb ring0cl on1el4 mpdan ) AHIDCIC
        JKLADDMDMNZTMOPABCDEFGQDABCEFRS $.
        $( [15-Feb-2010] $)
    $}
  $}

  ${
    $d H u x $.  $d X u x $.
    ring1cl.1 $e |- X = ran ( 1st ` R ) $.
    ring1cl.2 $e |- H = ( 2nd ` R ) $.
    ring1cl.3 $e |- U = ( Id ` H ) $.
    $( The unit of a ring belongs to the base set.  (Contributed by FL,
       12-Feb-2010.) $)
    ring1cl $p |- ( R e. RingOps -> U e. X ) $=
      ( cring wcel c2nd cfv crn cmagm cexid cin wa cmnd unmnd eleq1i mndismgm
      mndisexid jca sylbi syl elin sylibr eqid cgi fveq2i eqtri iorlid c1st
      wceq wb rnplrnml eqtr eleq2d sylancr mpbird ) AHIZBDIZBAJKZLZIZUTVBMNOIZV
      DUTVBMIZVBNIZPZVEUTCQIZVHACFRVIVBQIZVHCVBQFSVJVFVGVBTVBUAUBUCUDVBMNUEUFBV
      BVCVCUGBCUHKVBUHKGCVBUHFUIUJUKUDUTDAULKZLZUMZVLVCUMZVAVDUNEAVKVBVBUGVKUGU
      OVMVNPDVCBDVLVCUPUQURUS $.
      $( [12-Feb-2010] $)
  $}

  ${
    $d R x $.  $d U x $.  $d X x $.  $d Z x $.
    uznzr.1 $e |- G = ( 1st ` R ) $.
    uznzr.2 $e |- H = ( 2nd ` R ) $.
    uznzr.3 $e |- Z = ( Id ` G ) $.
    uznzr.4 $e |- U = ( Id ` H ) $.
    uznzr.5 $e |- X = ran G $.
    $( In a unital ring the zero equals the unity iff the ring is the zero
       ring.  (Contributed by FL, 14-Feb-2010.) $)
    uznzr $p |- ( R e. RingOps -> ( X ~~ 1o <-> U = Z ) ) $=
      ( vx cring wcel c1o cen wbr wceq wi ring0cl csn wa crn c1st cfv rneqi
      ring1cl eqcomi eleq2i eleq2 biimpd elsni syl6com sylbi syl en1eqsn
      syl5com ex com23 mpcom c0 wne rngn0 cv co wral ringorz r19.21aiva eqtri
      ringoidmlem simprd r19.26 eqtr eqcoms imp31 ralimi eqsn breq1 ensn1g
      syl5bir syl6bir com3l sylbir com24 mpd opreq2 adantr com13 impbid ) AMNZE
      OPQZBFRZFENZWJWKWLSACEFGKITZWMWKWJWLWMWKWJWLSWJEFUAZRZWLWMWKUBWJBCUCZNZWP
      WLSZABDWQCAUDUEZGUFZHJUGWRBENZWSWQEBEWQKUHUIWPXBBWONZWLWPXBXCEWOBUJUKBFUL
      UMUNUOFEUPUQURUSUTEVAVBZWJWLWKSACEGKVCWLWJXDWKWJLVDZBDVEZXEFDVEZRZLEVFZXD
      WKSZWLWJXGFRZLEVFZXIXJSZWJXKLEXEACDEFIKGHVGVHXFXERZLEVFZWJXLXMSWJXNLEWJXE
      ENZUBBXEDVEXERXNXEABDEHEWQWTUCKXAVIJVJVKVHXOXIXLWJXJXOXIXLWJXJSZSZXOXIUBX
      NXHUBZLEVFZXRXNXHLEVLXTXLXQXTXLUBXSXKUBZLEVFZXQXSXKLEVLYBXEFRZLEVFZXQYAYC
      LEXNXHXKYCXHXKYCSZSXEXFXEXFRZXHYEYFXHUBXEXGRZYEXEXFXGVMYGXKYCXEXGFVMURUOU
      RVNVOVPXDYDWJWKXDYDWPWJWKSLEFVQWPWKWOOPQZWJEWOOPVRWJWMYHWNFEVSUOVTWAWBUOW
      CURWCURWDUTWEWLXHLEWLXHXPBFXEDWFWGVHUQWHUTWI $.
      $( [14-Feb-2010] $)
  $}

  ${
    $d G g h $.  $d H g h $.  $d x y $.
    $( The predicate "is a division ring".  (Contributed by FL, 6-Sep-2009.) $)
    isdivrngo $p |- ( H e. A -> ( <. G , H >. e. DivRingOps
     <-> ( <. G , H >. e. RingOps /\ ( H |` (
       ( ran G \ { ( Id ` G ) } ) X. ( ran G \ { ( Id ` G ) } ) ) )
         e. GrpOp ) ) ) $=
      ( vx vy vg vh wcel cop cdrng cring crn cgi cfv csn cdif cxp cres cgr wa
      cvv wbr df-br wrel cv copab relopab df-drngo releqi mpbir brrelexi
      sylbir anim1i ancoms cabl fora elisset syl ad2antrl simpl jca wceq opeq1
      eleq1d rneq fveq2 sneqd difeq12 syl11anc xpeq12 reseq2d anbi12d opeq2
      reseq1 opelopabg eleq2i syl5bb pm5.21nd ) CAHZBCIZJHZVTKHZCBLZBMNZOZPZWFQ
      ZRZSHZTZBUAHZVSTZWAVSWLWAWKVSWABCJUBWKBCJUCBCJJUDDUEZEUEZIKHWNWMLWMMNOPZW
      OQRSHTZDEUFZUDWPDEUGJWQDEUHUIUJUKULUMUNVSWJTWKVSWBWKVSWIWBBUOHWKBCUPBUOUQ
      URUSVSWJUTVAWLVTFUEZGUEZIZKHZWSWRLZWRMNZOZPZXEQZRZSHZTZFGUFZHWJWAXIBWSIZK
      HZWSWGRZSHZTWJFGBCUAAWRBVBZXAXLXHXNXOWTXKKWRBWSVCVDXOXGXMSXOXFWGWSXOXEWFV
      BZXPXFWGVBXOXBWCVBXDWEVBXPWRBVEXOXCWDWRBMVFVGXBWCXDWEVHVIZXQXEWFXEWFVJVIV
      KVDVLWSCVBZXLWBXNWIXRXKVTKWSCBVMVDXRXMWHSWSCWGVNVDVLVOJXJVTFGUHVPVQVR $.
      $( [6-Sep-2009] $)
  $}

  ${
    $d A u x $.
    zrdivrng.1 $e |- A e. B $.
    $( The zero ring is not a division ring.  (Contributed by FL,
       24-Jan-2010.) $)
    zrdivrng $p |- -.
      <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >.
         e. DivRingOps $=
      ( vu vx cop csn cvv wcel cdrng wn snex cring crn cgi cfv cdif cxp cres
      cgr wa c0 0ngrp opex elisseti rnsnop wceq grpsn cv co wral crab cuni
      eqcomi eqid grpoidval cab elsni adantr elsn biimpri wi wfn wfun cdm
      df-fn funsn dmsnop xpsn eqtr4i mpbir2an snid w3a fnotoprb mpbiri mp3an
      simpr opreq1d eqeq1d simpl opreq2d eqeq12d bitrd ex sylbi com12
      r19.21aiv jca impbii abbii df-rab df-sn 3eqtr4i unieqi unisn eqtri
      syl6eq sneqd ax-mp difeq12i difid xpeq1i xp0r reseq2i res0 eleq1i mtbir
      a1i intnand isdivrngo mtbird ) AAFZAFZGZHIZXNXNFZJIZKXMLXOXQXPMIZXNXNNZXN
      OPZGZQZYBRZSZTIZUAXOYEXRYEKXOYEUBTIUCYDUBTYDXNUBSUBYCUBXNYCUBYBRUBYBUBYBY
      BAGZYFQUBXSYFYAYFXLAAAUDZABCUEZUFZXNTIZYAYFUGAYHUHYJXTAYJXTDUIZEUIZXNUJZY
      LUGZEYFUKZDYFULZUMZAEDXTXNYFXSYFYIUNXTUOUPYQYFUMAYPYFYKYFIZYOUAZDUQYKAUGZ
      DUQYPYFYSYTDYSYTYRYTYOYKAURUSYTYRYOYRYTDAUTVAYTYNEYFYLYFIZYTYNUUAYLAUGZYT
      YNVBEAUTUUBYTYNUUBYTUAZYNAAXNUJZAUGZXNYFYFRZVCZAYFIZUUHUUEUUGXNVDXNVEZUUF
      UGXNUUFVFXLAYGYHVGUUIXLGUUFXLAVHAAYHYHVIVJVKAYHVLZUUJUUGUUHUUHVMUUEXMXNIX
      MXLAUDVLYFYFAAAXNYHVNVOVPUUCYNAYLXNUJZYLUGUUEUUCYMUUKYLUUCYKAYLXNUUBYTVQV
      RVSUUCUUKUUDYLAUUCYLAAXNUUBYTVTZWAUULWBWCVOWDWEWFWGWHWIWJYODYFWKDAWLWMWNA
      YHWOWPWQWRWSWTYFXAWPXBYBXCWPXDXNXEWPXFXGXHXIHXNXNXJXKWS $.
      $( [24-Jan-2010] $)
  $}

  ${
    dvrunz.1 $e |- G = ( 1st ` R ) $.
    dvrunz.2 $e |- H = ( 2nd ` R ) $.
    dvrunz.3 $e |- X = ran G $.
    dvrunz.4 $e |- Z = ( Id ` G ) $.
    dvrunz.5 $e |- U = ( Id ` H ) $.
    $( In a division ring the unit is different from the zero.  (Contributed by
       FL, 14-Feb-2010.) $)
    dvrunz $p |- ( R e. DivRingOps -> U =/= Z ) $=
      ( cdrng wcel cop csn wn wne cvv cgi cfv fvex eqeltri zrdivrng wceq c1o
      cen wbr cring wb cdif cxp cres cgr drngi simpld uznzr syl wi on1el6
      eleq1 biimpd syl6bi pm2.43a sylbird necon3bd mpi ) ALMZFFNFNOZVHNZLMZPBFQ
      FRFCSTRJCSUAUBUCVGVJBFVGBFUDZEUEUFUGZVJVGAUHMZVLVKUIVGVMDEFOUJZVNUKULUMMA
      CDEFGHIJUNUOZABCDEFGHJKIUPUQVLVGVJVGVLAVIUDZVGVJURVGVMVLVPUIVOACEFGIJUSUQ
      VPVGVJAVILUTVAVBVCVDVEVF $.
      $( [14-Feb-2010] $)
  $}


