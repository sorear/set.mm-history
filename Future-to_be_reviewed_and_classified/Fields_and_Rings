$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Fields and Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Com2 $.

  $( Extend class notation with a class that adds commutativity to various
     flavors of rings. $)
  ccm2 $a class Com2 $.


  ${
    $d g h a b $.
    $( A device to add commutativity to various sorts of rings.  I use
       ` ran g ` because I suppose ` g ` has a neutral element and therefore is
       onto. $)
    df-com2 $a |- Com2 = { <. g , h >. | A. a e. ran g A. b e. ran g
     ( a h b ) = ( b h a ) } $.
  $}

  ${
    $d G a b x y $.  $d H a b x y $.
    $( A device to add commutativity to various sorts of rings.  (Contributed
       by FL, 6-Sep-2009.) $)
    iscom2 $p |- ( ( G e. A /\ H e. B ) -> ( <. G , H >. e. Com2 <->
     A. a e. ran G A. b e. ran G ( a H b ) = ( b H a ) ) ) $=
      ( vy vx wcel wa cop ccm2 cv co wceq crn wral copab raleqbidv opreq eleq2d
      df-com2 a1i rneq raleqdv eqidd eqeq12d opelopabg bitrd ) CAIDBIJZCDKZLIUK
      EMZFMZGMZNZUMULUNNZOZFHMZPZQZEUSQZHGRZIULUMDNZUMULDNZOZFCPZQZEVFQZUJLVBUK
      LVBOUJHGEFUBUCUAVAUQFVFQZEVFQVHHGCDABURCOZUTVIEUSVFURCUDZVJUQFUSVFVKUESUN
      DOZVIVGEVFVFVLVFUFZVLUQVEFVFVFVMVLUOVCUPVDULUMUNDTUMULUNDTUGSSUHUI $.
      $( [6-Sep-2009] $)
  $}

  $c Fld $.

  $( Extend class notation with the class of all fields. $)
  cfld $a class Fld $.

  $( Add alternate definition. Add Kennington's one (=/= the zero ring),
       and Mizar's one: ` ( Id `` G ) =/= ( Id `` H ) ` $)
  $( Definition of a field.  A field is a commutative division ring. $)
  df-fld $a |- Fld = ( DivRingOps i^i Com2 ) $.

  $( A field is a division ring.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
  flddivrng $p |- ( K e. Fld -> K e. DivRingOps ) $=
    ( cfld cdrng ccm2 cin df-fld inss1 eqsstri sseli ) BCABCDECFCDGHI $.
    $( [15-Dec-2013] $) $( [10-Jun-2010] $)

  ${
    rngn0.1 $e |- G = ( 1st ` R ) $.
    rngn0.2 $e |- X = ran G $.
    $( The base set of a ring is not empty.  (Contributed by FL,
       24-Jan-2010.) $)
    rngn0 $p |- ( R e. RingOps -> X =/= (/) ) $=
      ( cring wcel cgr c0 wne ringogrpo grpon0 syl ) AFGBHGCIJABDKBCELM $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d G u x y $.  $d X u x y $.
    $( The range of an internal operation with a left and right identity
       element equals its base set.  (Contributed by FL, 24-Jan-2010.) $)
    rngmgmbs4 $p |- ( ( G : ( X X. X ) --> X /\
      E. u e. X A. x e. X ( ( u G x ) = x /\ ( x G u ) = x ) )
         -> ran G = X ) $=
      ( vy cxp wf cv co wceq wa wral wrex wfo crn r19.12 wcel simpl eqcomd syl
      opreq2 eqeq2d rcla4ev ex syl5 reximdv ralimia anim2i fooprv sylibr forn )
      DDFZDCGZBHZAHZCIZUOJZUOUNCIUOJZKZADLBDMZKZULDCNZCODJVAUMUOUNEHZCIZJZEDMZB
      DMZADLZKVBUTVHUMUTUSBDMZADLVHUSBADDPVIVGADUODQZUSVFBDUSUOUPJZVJVFUSUPUOUQ
      URRSVJVKVFVEVKEUODVCUOJVDUPUOVCUOUNCUAUBUCUDUEUFUGTUHBEADDDCUIUJULDCUKT
      $.
      $( [22-Dec-2013] $) $( [24-Jan-2010] $)
  $}

  ${
    rnplrnml0.1 $e |- H = ( 2nd ` R ) $.
    rnplrnml0.2 $e |- G = ( 1st ` R ) $.
    $( In a unital ring the domain of the first variable of the addition equals
       the domain of the first variable of the multiplication.  (Contributed by
       FL, 24-Jan-2010.) $)
    rnplrnml0 $p |- ( R e. RingOps -> dom dom G = dom dom H ) $=
      ( cring wcel crn cxp wf cdm wceq eqid ringsm cgr wfo ringogrpo fdm 3syl
      wi grpfo fof wa eqtr dmeqd expcom eqcoms syl com12 mpd ) AFGZBHZULIZULCJZ
      BKZKCKZKLZABCULEDULMZNUKBOGUMULBPZUNUQTZABEQBULURUAUSUMULBJUOUMLZUTUMULBU
      BUMULBRUNVAUQUNUPUMLVAUQTZUMULCRVBUMUPVAUMUPLZUQVAVCUCUOUPUOUMUPUDUEUFUGU
      HUISSUJ $.
      $( [24-Jan-2010] $)

    $( In a unital ring the range of the addition equals the domain of the
       first variable of the multiplication.  (Contributed by FL,
       24-Jan-2010.) $)
    rnplrnml2 $p |- ( R e. RingOps -> ran G = dom dom H ) $=
      ( cring wcel crn cdm cgr wceq ringogrpo grprndm syl rnplrnml0 eqtrd ) AFG
      ZBHZBIIZCIIQBJGRSKABELBMNABCDEOP $.
      $( [24-Jan-2010] $)

    ${
      $d G x y z $.  $d H x y z $.  $d R x $.
      $( In a unital ring the range of the addition equals the range of the
         multiplication.  (Contributed by FL, 24-Jan-2010.) $)
      rnplrnml $p |- ( R e. RingOps -> ran G = ran H ) $=
        ( vx vy vz cring wcel crn cxp wf cv co wceq wa wral wrex eqid rngmgmbs4
        ringsm cabl w3a ringoi simprr syl syl11anc eqcomd ) AIJZCKZBKZUJULULLUL
        CMZFNZGNZCOZUOPUOUNCOUOPQGULRFULSZUKULPABCULEDULTZUBUJBUCJUMQZUPHNZCOUN
        UOUTCOZCOPUNUOUTBOCOUPUNUTCOZBOPUNUOBOUTCOVBVABOPUDHULRGULRFULRZUQQQUQF
        GHABCULEDURUEUSVCUQUFUGGFCULUAUHUI $.
        $( [24-Jan-2010] $)
    $}
  $}

  ${
    $d H x y z $.  $d R x y z $.
    unmnd.1 $e |- H = ( 2nd ` R ) $.
    $( In a unital ring the multiplication is a monoid.  (Contributed by FL,
       24-Jan-2010.) $)
    unmnd $p |- ( R e. RingOps -> H e. Mnd ) $=
      ( vx vy vz wcel cdm cv co wceq wral wa w3a eqid wi wb eleq2 ralbidv2 cvv
      cring cxp wf wrex cmnd c1st cfv crn ringsm ringoass expcom 3exp com34 imp
      impcom ralrimiv ralrimivv cabl ringoi simprd 3jca rnplrnml2 biimpri feq23
      ex xpid11 mpancom imbi1d imbi12d anbi12d rexbidv2 3anbi123d eqcoms mpbird
      syl c2nd fvex eleq1 mpbiri ismnd1 ax-mp sylibr ) AUAGZBHHZWDUBZWDBUCZDIZE
      IZBJZFIZBJWGWHWJBJZBJKZFWDLZEWDLZDWDLZWIWHKWHWGBJWHKMZEWDLZDWDUDZNZBUEGZW
      CWSAUFUGZUHZXBUBZXBBUCZWLFXBLZEXBLZDXBLZWPEXBLZDXBUDZNZWCXDXGXIAXABXBXAOZ
      CXBOZUIWCXEDEXBXBWCWGXBGZWHXBGZMZXEWCXOMWLFXBXOWCWJXBGZWLPZXMXNWCXQPXMXNX
      PWCWLXMXNXPWCWLPWCXMXNXPNWLWGWHWJAXABXBXKCXLUJUKULUMUNUOUPVEUQWCWLWGWHWJX
      AJBJWIWGWJBJZXAJKWGWHXAJWJBJXRWKXAJKNFXBLEXBLDXBLZXIWCXAURGXDMXSXIMDEFAXA
      BXBXKCXLUSUTUTVAWCXBWDKWSXJQZAXABCXKVBXTWDXBWDXBKZWFXDWOXGWRXIWEXCKZYAWFX
      DQYBYAWDXBVFVCWEWDXCXBBVDVGYAWNXFDWDXBYAWGWDGZXMWNXFWDXBWGRZYAWMXEEWDXBYA
      WHWDGZXNWMXEWDXBWHRZYAWLWLFWDXBYAWJWDGXPWLWDXBWJRVHSVISVISYAWQXHDWDXBYAYC
      XMWQXHYDYAWPWPEWDXBYAYEXNWPYFVHSVJVKVLVMVOVNBAVPUGZKZWTWSQZCYHBTGZYIYHYJY
      GTGAVPVQBYGTVRVSDEFTBWDWDOVTVOWAWB $.
      $( [22-Dec-2013] $) $( [24-Jan-2010] $)
  $}

  ${
    fora1.1 $e |- G e. A $.
    $( In a unital ring the addition is an abelian group.  (Contributed by FL,
       31-Aug-2009.) $)
    fora1 $p |- ( <. G , H >. e. RingOps -> G e. AbelOp ) $=
      ( cop c1st cfv elisseti op1st eqcomi ringoablo ) BCEZBLFGBBCBADHIJK $.
      $( [31-Aug-2009] $)
  $}

  $( In a unital ring the addition is an abelian group.  (Contributed by FL,
     31-Aug-2009.) $)
  fora2 $p |- ( ( G e. A /\ <. G , H >. e. RingOps ) -> G e. AbelOp ) $=
    ( wcel cop cring cabl cvv wi elisset csn cif wceq opeq1 eleq1d imbi12d snex
    eleq1 elimel fora1 dedth syl imp ) BADZBCEZFDZBGDZUDBHDZUFUGIZBAJUHUIUHBAAE
    AEZKZLZCEZFDZULGDZIBUKBULMZUFUNUGUOUPUEUMFBULCNOBULGRPHULCBUKHUJQSTUAUBUC
    $.
    $( [31-Aug-2009] $)

  $( In a unital ring the addition is an abelian group.  (Contributed by FL,
     31-Aug-2009.) $)
  fora $p |- ( <. G , H >. e. RingOps -> G e. AbelOp ) $=
    ( cvv wcel cop cring cabl wbr df-br relrng brrelexi sylbir fora2 mpancom )
    ACDZABEFDZAGDPABFHOABFIABFJKLCABMN $.
    $( [31-Aug-2009] $)

  ${
    uridm.1 $e |- H = ( 2nd ` R ) $.
    uridm.2 $e |- X = ran ( 1st ` R ) $.
    ${
      uridm.3 $e |- U = ( Id ` H ) $.
      $( The unit of a ring is an identity element for the multiplication.
         (Contributed by FL, 18-Feb-2010.) $)
      ringoidmlem $p |- ( ( R e. RingOps /\ A e. X ) ->
        ( ( U H A ) = A /\ ( A H U ) = A ) ) $=
        ( cring wcel co wceq wa wi crn cmnd cmagm cexid eqid ex unmnd 3syl c1st
        cin mndmgmid cmpidelt cfv wb rnplrnml eqtr simpl eleq2d imbi1d syl mpan
        mpcom mpbird imp ) BIJZAEJZCADKALACDKALMZUSUTVANZADOZJZVANZUSDPJDQRUDJZ
        VEBDFUADUEVFVDVAACDVCVCSHUFTUBBUCUGZOZVCLZUSVBVEUHZBVGDFVGSUIEVHLZVIUSV
        JNZGVKVIMEVCLZVLEVHVCUJVMUSVJVMUSMZUTVDVAVNEVCAVMUSUKULUMTUNUOUPUQUR $.
        $( [18-Feb-2010] $)

      $( The unit of a ring is an identity element for the multiplication.
         (Contributed by FL, 18-Apr-2010.) $)
      ringolidm $p |- ( ( R e. RingOps /\ A e. X ) -> ( U H A ) = A ) $=
        ( cring wcel wa co wceq ringoidmlem simpld ) BIJAEJKCADLAMACDLAMABCDEFG
        HNO $.
        $( [18-Apr-2010] $)
    $}

    ${
      uridm2.2 $e |- U = ( Id ` H ) $.
      $( The unit of a ring is an identity element for the multiplication.
         (Contributed by FL, 18-Apr-2010.) $)
      ringoridm $p |- ( ( R e. RingOps /\ A e. X ) -> ( A H U ) = A ) $=
        ( cring wcel wa co wceq ringoidmlem simprd ) BIJAEJKCADLAMACDLAMABCDEFG
        HNO $.
        $( [18-Apr-2010] $)
    $}
  $}

  ${
    $d A x $.  $d R x $.  $d X x $.
    on1el3.1 $e |- G = ( 1st ` R ) $.
    on1el3.2 $e |- X = ran G $.
    $( The only unital ring with a base set consisting in one element is the
       zero ring.  (Contributed by FL, 13-Feb-2010.) $)
    on1el3 $p |- ( ( R e. RingOps /\ A e. B ) -> ( X = { A } <-> R =
       <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ) ) $=
      ( vx wcel cring csn wceq cop wi cxp wf crn wa syl6bi ax-mp wb sneq eqeq2d
      cv opeq12 anidms mpancom opeq12d bibi12d imbi2d xpid11 c2nd cfv c1st eqid
      sneqd ringsm cgr wfo ringogrpo rneqi eqtri grpfo fof w3a cvv feq23 visset
      3syl xpsn feq2 fconst2 biimpi opex syl6eq anim12d ex syl5bi pm2.43i exp3a
      com13 3imp a1i cdm wrel wss relrng relssdmrn xpss sstri sseli jctild syl6
      eqopi 3expd mp2d syl5bir fveq2 snex op1st eqtr mpan2 syl5eq eqcomi rnsnop
      rneq 3eqtr3g impbid1 vtoclg impcom ) ABICJIZEAKZLZCAAMZAMZKZXPMZLZUAZXKEH
      UDZKZLZCXTXTMZXTMZKZYEMZLZUAZNXKXSNHABXTALZYHXSXKYIYBXMYGXRYIYAXLEXTAUBUC
      YIYFXQCYIYEXPYEXPYIYDXOYCXNLZYIYDXOLYIYJXTXTAAUEUFYCXTXNAUEUGUPZYKUHUCUIU
      JXKYBYGYBEEOZYAYAOZLZXKYGEYAUKZXKYLECULUMZPZYLECUNUMZPZYNYGNCDYPEFYPUOGUQ
      XKYRURIYLEYRUSYSCYRYRUOUTYREEDQZYRQZGDYRFVAVBVCYLEYRVDVIXKYQYSYNYGXKYQYSY
      NVEZCVFVFOZIZYRYELZYPYELZRZRYGXKUUBUUGUUDUUBUUGNXKYQYSYNUUGYNYSYQUUGYNYSY
      QUUGYNYSYQRUUGNZYNYBYNUUHYOYNYBUUHYNYBRZYSUUEYQUUFUUIYSYMYAYRPZUUEYLEYMYA
      YRVGYMYCKZLZUUJUUENXTXTHVHZUUMVJZUULUUJUUKYAYRPZUUEYMUUKYAYRVKUUOYRUUKYAO
      ZYEUUOYRUUPLUUKXTYRUUMVLVMYCXTXTXTVNZUUMVJZVOSTSUUIYQYMYAYPPZUUFYLEYMYAYP
      VGUULUUSUUFNUUNUULUUSUUKYAYPPZUUFYMUUKYAYPVKUUTYPUUPYEUUTYPUUPLUUKXTYPUUM
      VLVMUURVOSTSVPVQVRVSVTWAWBWCJUUCCJJWDZJQZOZUUCJWEJUVCWFWGJWHTUVAUVBWIWJWK
      WLCYEYEWNWMWOWPWQYGYRYFUNUMZLZUUEYBCYFUNWRUVEUVDYELUUEYEYEYDWSWTYRUVDYEXA
      XBUUEUUAYEQEYAYRYEXFEUUADYRLZEUUALFUVFEYTUUAGDYRXFXCTXDYCXTUUQUUMXEXGVIXH
      XIXJ $.
      $( [13-Feb-2010] $)

    ${
      $d A x $.  $d R x $.  $d X x $.
      $( The only unital ring with one element is the zero ring.  (Contributed
         by FL, 14-Feb-2010.) $)
      on1el4 $p |- ( ( R e. RingOps /\ A e. X ) -> ( X ~~ 1o <-> R =
       <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ) ) $=
        ( vx wcel c1o cen cop csn wceq wi wa ex syl c1st eqtr crn wbr wb opeq12
        cring anidms mpancom opeq12d eqeq2d bibi2d imbi2d en1eqsn on1el3 biimpd
        cv sneqd com13 pm2.43a com23 imp cfv fveq2 snex rneq opex visset rnsnop
        op1st ensn1 breq1 mpbiri a1d sylancl sylancr mpsyl com12 adantr vtoclga
        impbid impcom ) ADHBUDHZDIJUAZBAAKZAKZLZWDKZMZUBZVTWABGUNZWHKZWHKZLZWKK
        ZMZUBZNVTWGNGADWHAMZWNWGVTWOWMWFWAWOWLWEBWOWKWDWKWDWOWJWCWIWBMZWOWJWCMW
        OWPWHWHAAUCUEWIWHWBAUCUFUOZWQUGUHUIUJWHDHZVTWNWRVTOWAWMWRVTWAWMNWRWAVTW
        MWAWRVTWMNZWRWAWRWSNZWRWAODWHLZMZWTWHDUKVTWRXBWMVTWRXBWMNVTWROXBWMWHDBC
        DEFULUMPUPQPUQURUSWRWMWANVTWMWRWAWMBRUTZWLRUTZMZXDWKMZWRWANZBWLRVAWKWKW
        JVBVGXEXFOXCWKMZXGXCXDWKSCXCMZXHXCTZWKTZMZXGEXCWKVCXIDCTZMZXMXJMZXLXGNZ
        FCXCVCXNXOODXJMZXPDXMXJSXQXLXGXQXLODXKMZXKXAMZXGDXJXKSWIWHWHWHVDGVEZVFX
        RXSOXBXGDXKXASXBWAWRXBWAXAIJUAWHXTVHDXAIJVIVJVKQVLPQVMVNQVLVOVPVRPVQVS
        $.
        $( [14-Feb-2010] $)
    $}

    ${
      on1el3.3 $e |- Z = ( Id ` G ) $.
      $( The only unital ring with one element is the zero ring.  (Contributed
         by FL, 15-Feb-2010.) $)
      on1el6 $p |- ( R e. RingOps -> ( X ~~ 1o <-> R =
        <. { <. <. Z , Z >. , Z >. } , { <. <. Z , Z >. , Z >. } >. ) ) $=
        ( cring wcel c1o cen wbr cop csn wceq wb ring0cl on1el4 mpdan ) AHIDCIC
        JKLADDMDMNZTMOPABCDEFGQDABCEFRS $.
        $( [15-Feb-2010] $)
    $}
  $}

  ${
    $d H u x $.  $d X u x $.
    ring1cl.1 $e |- X = ran ( 1st ` R ) $.
    ring1cl.2 $e |- H = ( 2nd ` R ) $.
    ring1cl.3 $e |- U = ( Id ` H ) $.
    $( The unit of a ring belongs to the base set.  (Contributed by FL,
       12-Feb-2010.) $)
    ring1cl $p |- ( R e. RingOps -> U e. X ) $=
      ( cring wcel c2nd cfv crn cmagm cexid wa cmnd syl eqid cgi wceq cin unmnd
      eleq1i mndismgm mndisexid jca sylbi elin sylibr fveq2i iorlid wb rnplrnml
      eqtri c1st eqtr eleq2d sylancr mpbird ) AHIZBDIZBAJKZLZIZUTVBMNUAIZVDUTVB
      MIZVBNIZOZVEUTCPIZVHACFUBVIVBPIZVHCVBPFUCVJVFVGVBUDVBUEUFUGQVBMNUHUIBVBVC
      VCRBCSKVBSKGCVBSFUJUNUKQUTDAUOKZLZTZVLVCTZVAVDULEAVKVBVBRVKRUMVMVNODVCBDV
      LVCUPUQURUS $.
      $( [12-Feb-2010] $)
  $}


  ${
    $d R x $.  $d U x $.  $d X x $.  $d Z x $.
    uznzr.1 $e |- G = ( 1st ` R ) $.
    uznzr.2 $e |- H = ( 2nd ` R ) $.
    uznzr.3 $e |- Z = ( Id ` G ) $.
    uznzr.4 $e |- U = ( Id ` H ) $.
    uznzr.5 $e |- X = ran G $.
    $( In a unital ring the zero equals the unity iff the ring is the zero
       ring.  (Contributed by FL, 14-Feb-2010.) $)
    uznzr $p |- ( R e. RingOps -> ( X ~~ 1o <-> U = Z ) ) $=
      ( vx wcel c1o wceq wi wa syl ex wral cen wbr ring0cl csn en1eqsn crn c1st
      cring rneqi ring1cl eqcomi eleq2i eleq2 elsni syl6com sylbi syl5com com23
      cfv biimpd mpcom c0 wne rngn0 cv co opreq2 adantr ralrimiva ringorz eqtri
      ringoidmlem simprd r19.26 eqtr eqcoms imp31 ralimi ensn1g syl5ibr syl6bir
      eqsn breq1 com3l sylbir com24 mpd com13 impbid ) AUHMZENUAUBZBFOZFEMZWJWK
      WLPACEFGKIUCZWMWKWJWLWMWKWJWLPWMWKQEFUDZOZWJWLFEUEWJBCUFZMZWPWLPZABDWQCAU
      GUSZGUIZHJUJWRBEMZWSWQEBEWQKUKULWPXBBWOMZWLWPXBXCEWOBUMUTBFUNUOUPRUQSURVA
      EVBVCZWJWLWKPACEGKVDWLWJXDWKWLLVEZBDVFZXEFDVFZOZLETZWJXDWKPZWLXHLEWLXHXEE
      MZBFXEDVGVHVIWJXGFOZLETZXIXJPZWJXLLEXEACDEFIKGHVJVIXFXEOZLETZWJXMXNPWJXOL
      EWJXKQBXEDVFXEOXOXEABDEHEWQWTUFKXAVKJVLVMVIXPXIXMWJXJXPXIXMWJXJPZPZXPXIQX
      OXHQZLETZXRXOXHLEVNXTXMXQXTXMQXSXLQZLETZXQXSXLLEVNYBXEFOZLETZXQYAYCLEXOXH
      XLYCXHXLYCPZPXEXFXEXFOZXHYEYFXHQXEXGOZYEXEXFXGVOYGXLYCXEXGFVOSRSVPVQVRXDY
      DWJWKXDYDWPWJWKPLEFWBWJWKWPWONUAUBZWJWMYHWNFEVSREWONUAWCVTWAWDRWESWESWFVA
      WGUQWHVAWI $.
      $( [14-Feb-2010] $)
  $}

  ${
    $d G g h $.  $d H g h $.  $d x y $.
    $( The predicate "is a division ring".  (Contributed by FL, 6-Sep-2009.) $)
    isdivrngo $p |- ( H e. A -> ( <. G , H >. e. DivRingOps
     <-> ( <. G , H >. e. RingOps /\ ( H |` (
       ( ran G \ { ( Id ` G ) } ) X. ( ran G \ { ( Id ` G ) } ) ) )
         e. GrpOp ) ) ) $=
      ( vx vy vg vh wcel cop cdrng cring crn cgi cfv cres cgr wa cv wceq eleq1d
      csn cdif cxp cvv df-br wrel copab relopab df-drngo releqi brrelexi sylbir
      wbr mpbir anim1i ancoms cabl fora elisset syl ad2antrl simpl eleq2i opeq1
      rneq fveq2 sneqd difeq12 syl11anc xpeq12d reseq2d anbi12d opeq2 opelopabg
      jca reseq1 syl5bb pm5.21nd ) CAHZBCIZJHZVTKHZCBLZBMNZUAZUBZWFUCZOZPHZQZBU
      DHZVSQZWAVSWLWAWKVSWABCJUMWKBCJUEBCJJUFDRZERZIKHWNWMLWMMNUAUBZWOUCOPHQZDE
      UGZUFWPDEUHJWQDEUIUJUNUKULUOUPVSWJQWKVSWBWKVSWIWBBUQHWKBCURBUQUSUTVAVSWJV
      BVOWAVTFRZGRZIZKHZWSWRLZWRMNZUAZUBZXEUCZOZPHZQZFGUGZHWLWJJXJVTFGUIVCXIBWS
      IZKHZWSWGOZPHZQWJFGBCUDAWRBSZXAXLXHXNXOWTXKKWRBWSVDTXOXGXMPXOXFWGWSXOXEWF
      XEWFXOXBWCSXDWESXEWFSWRBVEXOXCWDWRBMVFVGXBWCXDWEVHVIZXPVJVKTVLWSCSZXLWBXN
      WIXQXKVTKWSCBVMTXQXMWHPWSCWGVPTVLVNVQVR $.
      $( [6-Sep-2009] $)
  $}

  ${
    zrdivrng.1 $e |- A e. _V $.
    $( The zero ring is not a division ring.  (Contributed by FL,
       24-Jan-2010.)  (The proof was shortened by Mario Carneiro,
       15-Dec-2013.) $)
    zrdivrng $p |- -.
      <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >.
         e. DivRingOps $=
      ( cop csn cdrng wcel c0 cgr 0ngrp crn cgi cfv cdif cxp cres opex elisseti
      cvv rnsnop eqtri gidsn sneqi difeq12i difid xpeq2i xp0 reseq2i res0 cring
      wa wb snex isdivrngo ax-mp simprbi syl5eqelr mto ) AACZACZDZUTCZEFZGHFIVB
      GUTUTJZUTKLZDZMZVFNZOZHVHUTGOGVGGUTVGVFGNGVFGVFVFADZVIMGVCVIVEVIURAAAPARB
      QSVDAABUAUBUCVIUDTUEVFUFTUGUTUHTVBVAUIFZVHHFZUTRFVBVJVKUJUKUSULRUTUTUMUNU
      OUPUQ $.
      $( [15-Dec-2013] $) $( [24-Jan-2010] $)
  $}

  ${
    dvrunz.1 $e |- G = ( 1st ` R ) $.
    dvrunz.2 $e |- H = ( 2nd ` R ) $.
    dvrunz.3 $e |- X = ran G $.
    dvrunz.4 $e |- Z = ( Id ` G ) $.
    dvrunz.5 $e |- U = ( Id ` H ) $.
    $( In a division ring the unit is different from the zero.  (Contributed by
       FL, 14-Feb-2010.) $)
    dvrunz $p |- ( R e. DivRingOps -> U =/= Z ) $=
      ( cdrng wcel cop csn wn cgi wceq wb syl wne cfv cvv fvex eqeltri zrdivrng
      c1o cen wbr cring cdif cxp cres drngi simpld uznzr wi on1el6 eleq1 biimpd
      cgr syl6bi pm2.43a sylbird necon3bd mpi ) ALMZFFNFNOZVHNZLMZPBFUAFFCQUBUC
      JCQUDUEUFVGVJBFVGBFRZEUGUHUIZVJVGAUJMZVLVKSVGVMDEFOUKZVNULUMVAMACDEFGHIJU
      NUOZABCDEFGHJKIUPTVLVGVJVGVLAVIRZVGVJUQVGVMVLVPSVOACEFGIJURTVPVGVJAVILUSU
      TVBVCVDVEVF $.
      $( [15-Dec-2013] $) $( [14-Feb-2010] $)
  $}

